/-
Copyright (c) 2025 Daniele Bolla. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Daniele Bolla, David Loeffler
-/
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Inverse
import Mathlib.Topology.Connected.PathConnected

/-!
# The "topologist's sine curve" is connected but not path-connected

We give an example of a closed subset `T` of `‚Ñù √ó ‚Ñù` which is connected but not path-connected: the
closure of the set `{ (x, sin x‚Åª¬π) | x ‚àà Ioi 0 }`.

## Main results

* `TopologistsSineCurve.T_is_conn`: the set `T` is connected.
* `TopologistsSineCurve.T_is_not_path_conn`: the set `T` is not path-connected.

This formalization is part of the UniDistance Switzerland bachelor thesis of Daniele Bolla. A
similar result has also been independently formalized by Vlad Tsyrklevich
(https://leanprover.zulipchat.com/#narrow/channel/113489-new-members/topic/golf.20request.3A.20Topologist's.20sine.20curve).
-/

open Topology Filter Set Real

namespace TopologistsSineCurve

/-- The topologist's sine curve, i.e. the graph of `y = sin (x‚Åª¬π)` for `0 < x`. -/
def S : Set (‚Ñù √ó ‚Ñù) := (fun x ‚Ü¶ (x, sin x‚Åª¬π)) '' Ioi 0

/-- The vertical line segment `{ (0, y) | -1 ‚â§ y ‚â§ 1 }`, which is the set of limit points of `S`
not contained in `S` itself. -/
def Z : Set (‚Ñù √ó ‚Ñù) := (fun y ‚Ü¶ (0, y)) '' Icc (-1) 1

/-- The union of `S` and `Z` (which we will show is the closure of `S`). -/
def T : Set (‚Ñù √ó ‚Ñù) := S ‚à™ Z

/-- A sequence of `x`-values tending to 0 at which the sine curve has a given `y`-coordinate. -/
noncomputable def x_seq (y : ‚Ñù) (k : ‚Ñï) := 1 / (arcsin y + (k + 1) * (2 * œÄ))

lemma x_seq_pos (y : ‚Ñù) (k : ‚Ñï) : 0 < x_seq y k := by
  rw [x_seq, one_div_pos]
  nlinarith [pi_pos, neg_pi_div_two_le_arcsin y]

lemma x_seq_invsin {y : ‚Ñù} (hy : y ‚àà Icc (-1) 1) (k : ‚Ñï) : sin (x_seq y k)‚Åª¬π = y := by
  simpa [x_seq, -Nat.cast_add, ‚Üê Nat.cast_succ] using sin_arcsin' hy

lemma x_seq_tendsto (y : ‚Ñù) : Tendsto (x_seq y) atTop (ùìù 0) := by
  refine .comp (g := fun k : ‚Ñù ‚Ü¶ 1 / (arcsin y + (k + 1) * (2 * œÄ))) ?_ tendsto_natCast_atTop_atTop
  simp only [div_eq_mul_inv, show ùìù 0 = ùìù (1 * (0 : ‚Ñù)) by simp]
  refine (tendsto_inv_atTop_zero.comp <| tendsto_atTop_add_const_left _ _ ?_).const_mul _
  exact (tendsto_atTop_add_const_right _ _ tendsto_id).atTop_mul_const (by positivity)

/-!
## `T` is closed
-/
/-- The closure of the topologist's sine curve `S` is the set `T`. -/
lemma closure_S : closure S = T := by
  ext ‚ü®x, y‚ü©
  -- Use sequential characterization of closure.
  simp only [mem_closure_iff_seq_limit, Prod.tendsto_iff]
  constructor
  ¬∑ -- Show that if a sequence in `S` has a limit in `‚Ñù ^ 2`, the limit must be in `T`.
    rintro ‚ü®f, hf_mem, hf_lim‚ü©
    have x_nonneg : 0 ‚â§ x := by
      refine isClosed_Ici.mem_of_tendsto hf_lim.1 (.of_forall fun n ‚Ü¶ ?_)
      obtain ‚ü®y, hy‚ü© := hf_mem n
      simpa [‚Üê hy.2] using le_of_lt hy.1
    -- Case split on whether limit point `(x, y)` has `x = 0`.
    rcases x_nonneg.eq_or_lt with rfl | h
    ¬∑ -- If the limit has `x = 0`, show `y`-coord must be in `[-1, 1]` using closedness
      right
      simp only [Z, mem_image, Prod.mk.injEq, true_and, exists_eq_right]
      refine isClosed_Icc.mem_of_tendsto hf_lim.2 (.of_forall fun n ‚Ü¶ ?_)
      obtain ‚ü®y, hy‚ü© := hf_mem n
      simpa only [Function.comp_apply, ‚Üê hy.2] using sin_mem_Icc ..
    ¬∑ -- If the limit has `0 < x`, show `y`-coord must be `sin x‚Åª¬π` using continuity
      refine .inl ‚ü®x, h, ?_‚ü©
      simp only [Prod.mk.injEq, true_and]
      have : ContinuousAt (fun x ‚Ü¶ sin x‚Åª¬π) x :=
        continuous_sin.continuousAt.comp <| continuousAt_inv‚ÇÄ h.ne'
      refine tendsto_nhds_unique ?_ hf_lim.2
      convert this.tendsto.comp hf_lim.1 with n
      obtain ‚ü®y, hy‚ü© := hf_mem n
      simp [‚Üê hy.2]
  ¬∑ -- Show that every `p ‚àà T` is the limit of a sequence in `S`.
    rintro (hz | ‚ü®z, hz‚ü©)
    ¬∑ -- Point is in `S`: use constant sequence
      exact ‚ü®_, fun _ ‚Ü¶ hz, tendsto_const_nhds, tendsto_const_nhds‚ü©
    ¬∑ -- Point is in `Z`: use sequence from `x_seq`
      simp only [Prod.mk.injEq] at hz
      rcases hz with ‚ü®hz, ‚ü®rfl, rfl‚ü©‚ü©
      refine ‚ü®fun n ‚Ü¶ (x_seq z n, z), fun n ‚Ü¶ ‚ü®_, x_seq_pos z n, ?_‚ü©, x_seq_tendsto z,
        tendsto_const_nhds‚ü©
      simpa using x_seq_invsin hz n

lemma isClosed_T : IsClosed T := by simpa only [‚Üê closure_S] using isClosed_closure

/-!
## `T` is connected
-/
/-- `T` is connected, being the closure of the set `S` (which is obviously connected since it
is a continuous image of the positive real line). -/
theorem T_is_conn : IsConnected T := by
  rw [‚Üê closure_S]
  refine (isConnected_Ioi.image _ <| continuousOn_id.prodMk ?_).closure
  exact continuous_sin.comp_continuousOn <| continuousOn_inv‚ÇÄ.mono fun _ hx ‚Ü¶ hx.ne'

/-!
## `T` is not path-connected
-/

lemma zero_mem_T : (0, 0) ‚àà T := by
  refine .inr ‚ü®0, ‚ü®?_, ?_‚ü©, rfl‚ü© <;>
  linarith

/-- A point in the `body` of the topologist's sine curve. -/
noncomputable def w : ‚Ñù √ó ‚Ñù := (1, sin 1‚Åª¬π)

lemma w_mem_T : w ‚àà T := .inl ‚ü®1, ‚ü®zero_lt_one' ‚Ñù, rfl‚ü©‚ü©

private lemma norm_ge_abs_snd {a b : ‚Ñù} : |b| ‚â§ ‚Äñ(a, b)‚Äñ := by simp

private lemma exists_unitInterval_gt {t‚ÇÄ : unitInterval} (ht‚ÇÄ : t‚ÇÄ < 1) {Œ¥ : ‚Ñù} (hŒ¥ : 0 < Œ¥) :
    ‚àÉ t‚ÇÅ, t‚ÇÄ < t‚ÇÅ ‚àß dist t‚ÇÄ t‚ÇÅ < Œ¥ := by
  let s‚ÇÄ := (t‚ÇÄ : ‚Ñù) -- t‚ÇÄ is in unitInterval
  let s‚ÇÅ := min (s‚ÇÄ + Œ¥/2) 1
  have h_s‚ÇÄ_delta_pos : 0 ‚â§ s‚ÇÄ + Œ¥/2 := add_nonneg t‚ÇÄ.2.1 (by positivity)
  have hs‚ÇÅ : 0 ‚â§ s‚ÇÅ := le_min h_s‚ÇÄ_delta_pos zero_le_one
  have hs‚ÇÅ': s‚ÇÅ ‚â§ 1 := min_le_right ..
  refine ‚ü®‚ü®s‚ÇÅ, hs‚ÇÅ, hs‚ÇÅ'‚ü©, lt_min ((lt_add_iff_pos_right _).mpr (half_pos hŒ¥)) ht‚ÇÄ, ?_‚ü©
  have h_le : s‚ÇÅ ‚â§ s‚ÇÄ + Œ¥/2 := min_le_left _ _
  have h_ge : s‚ÇÄ ‚â§ s‚ÇÅ := le_min (by linarith) t‚ÇÄ.2.2
  rw [Subtype.dist_eq, dist_comm, dist_eq, abs_of_nonneg (by linarith)]
  linarith

/-- The set `T` is not path-connected. -/
theorem T_is_not_path_conn : ¬¨ IsPathConnected T := by
  -- **Step 1**:
  -- Assume for contradiction we have a path from `z = (0, 0)` to `w = (1, sin 1)`.
  -- Let t‚ÇÄ be the last time the path is on the y-axis. By continuity of the path, we
  -- can find a `Œ¥ > 0` such that for all `t ‚àà [t‚ÇÄ, t‚ÇÄ + Œ¥]`, we have `‚Äñp(t) - p(t‚ÇÄ)‚Äñ < 1`.
  intro h_pathConn
  replace h_pathConn := h_pathConn.joinedIn (0, 0) zero_mem_T w w_mem_T
  let p := h_pathConn.somePath
  have xcoord_pathContinuous : Continuous fun t ‚Ü¶ (p t).1 := continuous_fst.comp p.continuous
  let t‚ÇÄ : unitInterval := sSup {t | (p t).1 = 0}
  have h_pt‚ÇÄ_x : (p t‚ÇÄ).1 = 0 :=
    (isClosed_singleton.preimage xcoord_pathContinuous).sSup_mem ‚ü®0, by aesop‚ü©
  obtain ‚ü®Œ¥ , hŒ¥, ht‚ü© : ‚àÉ Œ¥, 0 < Œ¥ ‚àß ‚àÄ t, dist t t‚ÇÄ < Œ¥ ‚Üí dist (p t) (p t‚ÇÄ) < 1 :=
    Metric.eventually_nhds_iff.mp <| Metric.tendsto_nhds.mp (p.continuousAt t‚ÇÄ) _ one_pos
  -- **Step 2**:
  -- Choose a time t‚ÇÅ in (t‚ÇÄ, t‚ÇÄ + Œ¥) and let `a = x(p(t‚ÇÅ))`. Using the fact that every
  -- connected subset of `‚Ñù` is an interval, we have `[0, a] ‚äÇ x(p([t0, t1]))`.
  obtain ‚ü®t‚ÇÅ, ht‚ÇÅ‚ü© : ‚àÉ t‚ÇÅ, t‚ÇÄ < t‚ÇÅ ‚àß dist t‚ÇÄ t‚ÇÅ < Œ¥ := by
    refine exists_unitInterval_gt (lt_of_le_of_ne (unitInterval.le_one t‚ÇÄ) fun ht‚ÇÄ' ‚Ü¶ ?_) hŒ¥
    have w_x_path : (p 1).1 = 1 := by simp [w]
    have x_eq_zero : (p 1).1 = 0 := by rwa [ht‚ÇÄ'] at h_pt‚ÇÄ_x
    linarith
  let a := (p t‚ÇÅ).1
  have ha : 0 < a := by
    obtain ‚ü®x, hxI, hx_eq‚ü© : p t‚ÇÅ ‚àà S := by
      refine (h_pathConn.somePath_mem t‚ÇÅ).elim id fun ‚ü®y, hy‚ü© ‚Ü¶ ?_
      have : (p t‚ÇÅ).1 = 0 := by simp only [p, ‚Üê hy.2]
      exact ((show t‚ÇÅ ‚â§ t‚ÇÄ from le_sSup this).not_lt ht‚ÇÅ.1).elim
    simpa only [a, ‚Üê hx_eq] using hxI
  have intervalAZeroSubOfT‚ÇÄT‚ÇÅXcoord : Icc 0 a ‚äÜ (fun t ‚Ü¶ (p t).1) '' Icc t‚ÇÄ t‚ÇÅ :=
    (isPreconnected_Icc.image _ <| xcoord_pathContinuous.continuousOn).Icc_subset
      (show 0 ‚àà (fun t ‚Ü¶ (p t).1) '' Icc t‚ÇÄ t‚ÇÅ from ‚ü®t‚ÇÄ, ‚ü®le_rfl, ht‚ÇÅ.1.le‚ü©, ‚Äπ_‚Ä∫‚ü©)
      (show a ‚àà (fun t ‚Ü¶ (p t).1) '' Icc t‚ÇÄ t‚ÇÅ from ‚ü®t‚ÇÅ, ‚ü®ht‚ÇÅ.1.le, le_rfl‚ü©, rfl‚ü©)
  -- **Step 3**:
  -- For every `y ‚àà [-1, 1]`, there exists a `t` with `p t = y` and `t ‚àà [t‚ÇÄ, t‚ÇÅ]`, implying
  -- that `dist t‚ÇÄ t < Œ¥`.
  -- first find an appropriate `t`-value
  have exists_in_interval {y : ‚Ñù} (hy : y ‚àà Icc (-1) 1) : ‚àÉ t ‚àà Icc t‚ÇÄ t‚ÇÅ, (p t).2 = y := by
    obtain ‚ü®i, hi‚ü© :  ‚àÉ i, x_seq y i ‚àà Icc 0 a := by
      obtain ‚ü®N, h_dist‚ü© := (Metric.tendsto_nhds.mp (x_seq_tendsto y) (a/2) (by positivity)).exists
      refine ‚ü®N, (x_seq_pos y N).le, ?_‚ü©
      rw [dist_eq, sub_zero, abs_of_pos (x_seq_pos _ N)] at h_dist
      linarith
    obtain ‚ü®t, ht‚ü© := intervalAZeroSubOfT‚ÇÄT‚ÇÅXcoord hi
    refine ‚ü®t, ht.1, ?_‚ü©
    suffices (p t).2 = sin (x_seq y i)‚Åª¬π from this ‚ñ∏ x_seq_invsin hy i
    obtain ‚ü®xpos_real, hxInpos_real, h_eq_path‚ü© : p t ‚àà S := by
      cases h_pathConn.somePath_mem t with
      | inl hS => exact hS
      | inr hZ =>
        obtain ‚ü®y, hy‚ü© := hZ
        have h_x_zero : (p t).1 = 0 := by simp [p, ‚Üê hy.2]
        have h_x_pos : 0 < (p t).1 := by simpa [ht.2] using x_seq_pos ..
        linarith
    have xIsSeq : xpos_real = x_seq y i := (congrArg Prod.fst h_eq_path).trans ht.2
    rw [‚Üê h_eq_path, xIsSeq]
  -- then show it lies within `Œ¥` of `t‚ÇÄ`.
  have exists_close (y : ‚Ñù) (hy : y ‚àà Icc (-1) 1) : ‚àÉ t, dist t t‚ÇÄ < Œ¥ ‚àß (p t).2 = y := by
    obtain ‚ü®t, ht, hty‚ü© : ‚àÉ t ‚àà Icc t‚ÇÄ t‚ÇÅ, (p t).2 = y := exists_in_interval hy
    refine ‚ü®t, ?_, hty‚ü©
    calc
    dist t t‚ÇÄ ‚â§ dist t‚ÇÅ t‚ÇÄ := by
      simp only [Subtype.dist_eq, Real.dist_eq]
      rw [abs_of_nonneg, abs_of_nonneg]
      ¬∑ simpa using ht.2
      ¬∑ linarith [Subtype.coe_lt_coe.mpr ht‚ÇÅ.1]
      ¬∑ simpa using mod_cast ht.1
    _ = dist t‚ÇÄ t‚ÇÅ := by rw [dist_comm]
    _ < Œ¥ := ht‚ÇÅ.2
  -- **Step 4**:
  -- Now the final contradiction: there are times within `Œ¥` of `t‚ÇÄ` with `p t = 1`, and with
  -- `p t = -1`; but both must have distance `< 1` from `p t‚ÇÄ`, contradicting the triangle
  -- inequality.
  obtain ‚ü®x‚ÇÅ, hx‚ÇÅ, h_pathx‚ÇÅ‚ü© : ‚àÉ x‚ÇÅ, dist x‚ÇÅ t‚ÇÄ < Œ¥ ‚àß (p x‚ÇÅ).2 = 1 := exists_close _ (by simp)
  obtain ‚ü®x‚ÇÇ, hx‚ÇÇ, h_pathx‚ÇÇ‚ü© : ‚àÉ x‚ÇÇ, dist x‚ÇÇ t‚ÇÄ < Œ¥ ‚àß (p x‚ÇÇ).2 = -1 := exists_close _ (by simp)
  have : dist (p x‚ÇÅ) (p x‚ÇÇ) < 2 := by
    refine (dist_triangle_right _ _ (p t‚ÇÄ)).trans_lt ?_
    exact (add_lt_add (ht _ hx‚ÇÅ) (ht _ hx‚ÇÇ)).trans_eq (by norm_num)
  have := norm_ge_abs_snd.trans_lt this
  rw [h_pathx‚ÇÅ, h_pathx‚ÇÇ, abs_of_nonneg (by norm_num)] at this
  linarith

end TopologistsSineCurve
