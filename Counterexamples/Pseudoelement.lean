/-
Copyright (c) 2022 Riccardo Brasca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Riccardo Brasca
-/
import Mathlib.CategoryTheory.Abelian.Pseudoelements
import Mathlib.Algebra.Category.ModuleCat.Biproducts

/-!
# Pseudoelements and pullbacks
Borceux claims in Proposition 1.9.5 that the pseudoelement constructed in
`CategoryTheory.Abelian.Pseudoelement.pseudo_pullback` is unique. We show here that this claim is
false. This means in particular that we cannot have an extensionality principle for pullbacks in
terms of pseudoelements.

## Implementation details
The construction, suggested in https://mathoverflow.net/a/419951/7845, is the following.
We work in the category of `ModuleCat â„¤` and we consider the special case of `â„š âŠ â„š` (that is the
pullback over the terminal object). We consider the pseudoelements associated to `x : â„š âŸ¶ â„š âŠ â„š`
given by `t â†¦ (t, 2 * t)` and `y : â„š âŸ¶ â„š âŠ â„š` given by `t â†¦ (t, t)`.

## Main results
* `Counterexample.exist_ne_and_fst_eq_fst_and_snd_eq_snd` : there are two
  pseudoelements `x y : â„š âŠ â„š` such that `x â‰  y`, `biprod.fst x = biprod.fst y` and
  `biprod.snd x = biprod.snd y`.

## References
* [F. Borceux, *Handbook of Categorical Algebra 2*][borceux-vol2]
-/


open CategoryTheory.Abelian CategoryTheory CategoryTheory.Limits ModuleCat LinearMap

namespace Counterexample

noncomputable section

open CategoryTheory.Abelian.Pseudoelement

/-- `x` is given by `t â†¦ (t, 2 * t)`. -/
def x : Over (of â„¤ â„š âŠ of â„¤ â„š) :=
  Over.mk (biprod.lift (ğŸ™ _) (2 â€¢ ğŸ™ _))

/-- `y` is given by `t â†¦ (t, t)`. -/
def y : Over (of â„¤ â„š âŠ of â„¤ â„š) :=
  Over.mk (biprod.lift (ğŸ™ _) (ğŸ™ _))

/-- `biprod.fst â‰« x` is pseudoequal to `biprod.fst y`. -/
theorem fst_x_pseudo_eq_fst_y : PseudoEqual _ (app biprod.fst x) (app biprod.fst y) := by
  refine âŸ¨of â„¤ â„š, ğŸ™ _, ğŸ™ _, inferInstance, ?_, ?_âŸ©
  Â· exact (ModuleCat.epi_iff_surjective _).2 fun a => âŸ¨(a : â„š), rflâŸ©
  Â· dsimp [x, y]
    simp

/-- `biprod.snd â‰« x` is pseudoequal to `biprod.snd y`. -/
theorem snd_x_pseudo_eq_snd_y : PseudoEqual _ (app biprod.snd x) (app biprod.snd y) := by
  refine âŸ¨of â„¤ â„š, ğŸ™ _, 2 â€¢ ğŸ™ _, inferInstance, ?_, ?_âŸ©
  Â· refine (ModuleCat.epi_iff_surjective _).2 fun a => âŸ¨(show â„š from a) / 2, ?_âŸ©
    simpa only [two_smul] using add_halves (show â„š from a)
  Â· dsimp [x, y]
    refine ConcreteCategory.hom_ext _ _ fun a => ?_
    simp_rw [biprod.lift_snd]; rfl

-- Porting note: locally disable instance to avoid inferred/synthesized clash
attribute [-instance] AddCommGroup.toIntModule in
/-- `x` is not pseudoequal to `y`. -/
theorem x_not_pseudo_eq : Â¬PseudoEqual _ x y := by
  intro h
  replace h := ModuleCat.eq_range_of_pseudoequal h
  dsimp [x, y] at h
  let Ï† := biprod.lift (ğŸ™ (of â„¤ â„š)) (2 â€¢ ğŸ™ (of â„¤ â„š))
  have mem_range := mem_range_self Ï†.hom (1 : â„š)
  rw [h] at mem_range
  obtain âŸ¨a, haâŸ© := mem_range
  rw [â† ModuleCat.id_apply _ (Ï† (1 : â„š)), â† biprod.total, â† LinearMap.comp_apply,
    â† ModuleCat.hom_comp, Preadditive.comp_add] at ha
  let Ï€â‚ := (biprod.fst : of â„¤ â„š âŠ of â„¤ â„š âŸ¶ _)
  have haâ‚ := congr_arg Ï€â‚ ha
  rw [â† ModuleCat.comp_apply, â† ModuleCat.comp_apply] at haâ‚
  simp only [Ï€â‚, Ï†, biprod.lift_fst, biprod.lift_fst_assoc, Category.id_comp,
    biprod.lift_snd_assoc, Linear.smul_comp, Preadditive.add_comp, BinaryBicone.inl_fst,
    BinaryBicone.inr_fst, smul_zero, add_zero] at haâ‚
  let Ï€â‚‚ := (biprod.snd : of â„¤ â„š âŠ of â„¤ â„š âŸ¶ _)
  have haâ‚‚ := congr_arg Ï€â‚‚ ha
  rw [â† ModuleCat.comp_apply, â† ModuleCat.comp_apply] at haâ‚‚
  simp_all [Ï€â‚‚, Ï†]

attribute [local instance] Pseudoelement.setoid

open scoped Pseudoelement

/-- `biprod.fst âŸ¦xâŸ§ = biprod.fst âŸ¦yâŸ§`. -/
theorem fst_mk'_x_eq_fst_mk'_y :
    pseudoApply biprod.fst âŸ¦xâŸ§ = pseudoApply biprod.fst âŸ¦yâŸ§ :=
  Quotient.eq.2 fst_x_pseudo_eq_fst_y

/-- `biprod.snd âŸ¦xâŸ§ = biprod.snd âŸ¦yâŸ§`. -/
theorem snd_mk'_x_eq_snd_mk'_y :
    pseudoApply biprod.snd âŸ¦xâŸ§ = pseudoApply biprod.snd âŸ¦yâŸ§ :=
  Quotient.eq.2 snd_x_pseudo_eq_snd_y

-- Porting note: needs explicit type ascription `: Quotient <| Pseudoelement.setoid _`
-- for some reason the setoid instance isn't picked up automatically,
-- despite the local instance ~20 lines up
/-- `âŸ¦xâŸ§ â‰  âŸ¦yâŸ§`. -/
theorem mk'_x_ne_mk'_y : (âŸ¦xâŸ§ : Quotient <| Pseudoelement.setoid _) â‰  âŸ¦yâŸ§ :=
  fun h => x_not_pseudo_eq <| Quotient.eq'.1 h

/-- There are two pseudoelements `x y : â„š âŠ â„š` such that `x â‰  y`, `biprod.fst x = biprod.fst y` and
`biprod.snd x = biprod.snd y`. -/
theorem exist_ne_and_fst_eq_fst_and_snd_eq_snd :
    âˆƒ x y, -- Porting note: removed type ascription `: of â„¤ â„š âŠ of â„¤ â„š`, it gave an error about
           -- `Type` not having zero morphisms. jmc: I don't understand where the error came from
      x â‰  y âˆ§
        pseudoApply (biprod.fst : of â„¤ â„š âŠ of â„¤ â„š âŸ¶ _) x = pseudoApply biprod.fst y âˆ§
          pseudoApply biprod.snd x = pseudoApply biprod.snd y :=
  âŸ¨âŸ¦xâŸ§, âŸ¦yâŸ§, mk'_x_ne_mk'_y, fst_mk'_x_eq_fst_mk'_y, snd_mk'_x_eq_snd_mk'_yâŸ©

end

end Counterexample
