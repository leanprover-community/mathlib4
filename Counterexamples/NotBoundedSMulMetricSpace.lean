/-
Copyright (c) 2025 Yakov Pechersky. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yakov Pechersky
-/
import Mathlib.Analysis.InnerProductSpace.Basic
import Mathlib.Data.Real.StarOrdered
import Mathlib.Topology.MetricSpace.PiNat
import Mathlib.Analysis.Normed.Order.Basic

/-! # Metric spaces are not necessarily induced by a norm.

In a normed space, the distance must be translation invariant and
homogeneous (scaled by scalar multiplication).

There are examples of metric spaces where distances do not satisfy these properties,
and thus cannot even have `BoundedSMul` instances.

-/

namespace Counterexample

section NormInduced

variable {ğ•œ E : Type*} [NormedAddCommGroup E] [NormedField ğ•œ] [NormedSpace ğ•œ E]

/-- A distance induced by a normed space must be translation invariant. -/
lemma dist_translation_invariant (x y c : E) :
    dist (x + c) (y + c) = dist x y := by
  simp

/-- A distance induced by a normed space must be homogeneous, which means that it is scaled
by scalar multiplication. -/
lemma dist_homogeneous (c : ğ•œ) (x : E) :
    dist (c â€¢ x) 0 = â€–câ€– * dist x 0 := by
  simp [norm_smul]

end NormInduced

open PiCountable

variable {Î¹ : Type*} [Encodable Î¹] {F : Î¹ â†’ Type*}

noncomputable local instance [âˆ€ i, MetricSpace (F i)] :
    MetricSpace (Î  i, F i) :=
  PiCountable.metricSpace

@[simp]
lemma PiCountable.dist_translation_invariant [âˆ€ i, NormedAddCommGroup (F i)] (x y c : Î  i, F i) :
    dist (x + c) (y + c) = dist x y := by
  simp [dist_eq_tsum]

noncomputable instance [âˆ€ i, NormedAddCommGroup (F i)] : NormedAddCommGroup (Î  i, F i) where
  __ := PiCountable.metricSpace
  norm x := dist x 0
  dist_eq x y := by
    simpa [â† sub_eq_add_neg] using
      (PiCountable.dist_translation_invariant x y (-y)).symm

open Encodable

lemma PiCountable.norm_single [DecidableEq Î¹] [âˆ€ i, NormedAddCommGroup (F i)] (i : Î¹) (r : F i) :
    â€–(Pi.single i r : Î  i, F i)â€– = (2 ^ encode i)â»Â¹ âŠ“ â€–râ€– := by
  rw [â† sub_zero (Pi.single _ _), â† dist_eq_norm, dist_eq_tsum, tsum_eq_single i]
  Â· simp
  Â· simp +contextual [Pi.single_apply]

lemma PiCountable.not_dist_homogeneous' [DecidableEq Î¹]
    [âˆ€ i, NormedLinearOrderedField (F i)] [âˆ€ i, NormedSpace â„ (F i)] [âˆ€ i, BoundedSMul â„ (F i)]
     (i : Î¹) (hi : 0 < encode i) :
    Â¬ âˆ€ (x y : Î  i, F i) (r : â„),
    dist (r â€¢ x) (r â€¢ y) â‰¤ â€–râ€– * dist x y := by
  intro H
  specialize H (Pi.single i 1) 0 2â»Â¹
  refine H.not_lt ?_
  clear H
  have : (2 ^ encode i : â„)â»Â¹ < 1 := by
    rw [inv_lt_commâ‚€ (by simp) (by simp), inv_one]
    simpa using pow_lt_pow_rightâ‚€ one_lt_two hi
  simp only [norm_inv, RCLike.norm_ofNat, dist_eq_norm, sub_zero, norm_single, norm_one,
    min_eq_left this.le, â† Pi.single_smul, smul_zero, norm_smul, lt_inf_iff, inv_pos, Nat.ofNat_pos,
    pow_pos, mul_lt_iff_lt_one_left, two_inv_lt_one, true_and, gt_iff_lt]
  norm_num
  exact this

open PiCountable

/-- Not all distances on a metric space are induced by a norm. Phrased by remarking that
one can have a `MetricSpace` and even `NormedAddCommGroup` without `BoundedSMul`, which is
a prerequisite for `NormedSpace`. -/
theorem not_all_dist_induced_by_norm : Â¬ âˆ€ (ğ•œ E : Type) [MetricSpace ğ•œ] [MetricSpace E]
    [Zero ğ•œ] [Zero E] [SMul ğ•œ E], BoundedSMul ğ•œ E := by
  intro H
  obtain âŸ¨H, -âŸ© := H â„ (â„• â†’ â„)
  classical
  apply PiCountable.not_dist_homogeneous' (Î¹ := â„•) (F := fun _ â†¦ â„) 1 (by simp)
  intro f g r
  simpa using H r f g

end Counterexample
