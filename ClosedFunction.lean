/-
Copyright (c) 2023 Wanyi He. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Wanyi He
-/
import Mathlib.Topology.Sequences
import Mathlib.Order.LiminfLimsup
import Mathlib.Topology.Instances.EReal

/-!
# Closedness and Semicontinuity
A function `f` from a topolofical space `E` to `EReal` is said to be closed if its epigraph is closed.
Equivalently, `f` is closed if all its sublevel sets are closed.

## Main definitions and results

We introduce 2 definitions related to closed function:
* `ClosedOn f s`
* `Closed f`

We prove the equivalance of semi-continuity, closed functions and closedness of sublevel set.
* `closed_iff_IsClosed_sublevel`: a function is closed if and only if all its sublevel sets are closed.
* `closed_iff_LowerSemicontinuous`: a function is closed if and only if it is lower semi-continuous.
* `lowerSemicontinuous_iff_IsClosed_sublevel`: 
  a function is lower semi-continuous if and only if all its sublevel sets are closed.

## Implementation details

Although Mathlib4 already has a definition of semi-continuity, this file uses a more natural 
definition in mathematics when proving the equivalence of semi-continuity and closed functions.

## References

* <https://en.wikipedia.org/wiki/Closed_convex_function>

-/


open Topology Filter Set TopologicalSpace

variable {E Î± Î² Î¹ : Type _}

variable [AddCommMonoid E]

variable {s t : Set E} {f : E â†’ EReal}

variable [TopologicalSpace E] [FirstCountableTopology E]

/-- A function `f : E â†’ EReal` is said to be closed on a set `s` if its epigraph 
`{p : E Ã— EReal | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2}` is closed on `s`. -/
def ClosedOn (f : E â†’ EReal) (s : Set E) : Prop :=
  IsClosed {p : E Ã— EReal | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2}

/-- A function `f : E â†’ EReal` is said to be closed if its epigraph 
`{p : E Ã— EReal | f p.1 â‰¤ p.2}` is closed. -/
def Closed (f : E â†’ EReal) : Prop := 
  IsClosed {p : E Ã— EReal | f p.1 â‰¤ p.2}

theorem ClosedOn.closed_epigraph (hf : ClosedOn f s) : 
  IsClosed {p : E Ã— EReal | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2} := hf

theorem Closed.closed_epigraph (hf : Closed f) :
  IsClosed {p : E Ã— EReal | f p.1 â‰¤ p.2} := hf

theorem closed_univ_iff : ClosedOn f univ â†” Closed f := by 
  simp [ClosedOn, Closed]

/-!
### Equivalence
-/

theorem Closed.isClosed_sublevel {f : E â†’ EReal} (hf : Closed f) :
    âˆ€ (r : EReal), IsClosed {x | f x â‰¤ r} :=
  fun _ => IsSeqClosed.isClosed fun â¦ƒ_â¦„ â¦ƒ_â¦„ xns cx =>
    IsClosed.isSeqClosed hf (fun n => xns n) (Tendsto.prod_mk_nhds cx tendsto_const_nhds)

theorem LowerSemicontinuous.Closed {f : E â†’ EReal}
    (hf : âˆ€ x, f x â‰¤ (liminf f <| ð“ x)) : Closed f := by
  apply IsSeqClosed.isClosed
  intro f' âŸ¨x', y'âŸ© hxy cxy
  rw [Prod.tendsto_iff] at cxy
  let x : â„• -> E := fun (n : â„•) => (f' n).1
  have xley : âˆ€ (n : â„•), (f âˆ˜ x) n â‰¤ (f' n).snd :=
    fun n => by convert hxy n
  calc
    f x' â‰¤ liminf f (ð“ x') := hf x'
    _ â‰¤ liminf (f âˆ˜ x) atTop := by
      rw[liminf_eq, liminf_eq]
      exact sSup_le_sSup (fun _ fa => (eventually_iff_seq_eventually.mp fa) x cxy.1)
    _ â‰¤ liminf (fun (n : â„•) => (f' n).2) atTop := liminf_le_liminf (eventually_of_forall xley)
    _ = y' := (cxy.2).liminf_eq

theorem lowerSemicontinuous_of_isClosed_sublevel {f : E â†’ EReal}
    (h : âˆ€ r : EReal, IsClosed {x | f x â‰¤ r}) :
    âˆ€ x , f x â‰¤ (liminf f <| ð“ x) := by
  by_contra h; push_neg at h
  obtain âŸ¨x, hxâŸ© := h
  obtain âŸ¨t, âŸ¨ltt, tltâŸ©âŸ© := exists_between hx
  apply not_le_of_gt tlt
  apply isClosed_iff_frequently.mp (h t) x
  apply frequently_iff.mpr; intro _ hu
  have h : âˆƒá¶  (y : E) in ð“ x, f y â‰¤ t := by
    apply frequently_iff.mpr; intro _ hu
    obtain âŸ¨x, xu, fxâŸ© :=
      (frequently_iff.mp (frequently_lt_of_liminf_lt (by isBoundedDefault) ltt)) hu
    exact Exists.intro x âŸ¨xu, LT.lt.le fxâŸ©
  obtain âŸ¨x, xu, fxâŸ© := (frequently_iff.mp h) hu
  exact Exists.intro x âŸ¨xu, fxâŸ©

/-- A function is closed if and only if all its sublevel sets are closed. -/
theorem closed_iff_IsClosed_sublevel {f : E â†’ EReal} :
    Closed f â†” âˆ€ (r : EReal), IsClosed {x | f x â‰¤ r} :=
  âŸ¨fun h => Closed.isClosed_sublevel h,
    fun h => LowerSemicontinuous.Closed fun x => lowerSemicontinuous_of_isClosed_sublevel h xâŸ©

/-- A function is closed if and only if it is lower semi-continuous. -/
theorem closed_iff_LowerSemicontinuous {f : E â†’ EReal} :
    Closed f â†” âˆ€ x, f x â‰¤ (liminf f <| ð“ x) :=
  âŸ¨fun h => lowerSemicontinuous_of_isClosed_sublevel fun _ => Closed.isClosed_sublevel h _,
    fun a => LowerSemicontinuous.Closed aâŸ©

/-- A function is lower semi-continuous if and only if all its sublevel sets are closed. -/
theorem lowerSemicontinuous_iff_IsClosed_sublevel {f : E â†’ EReal} :
    (âˆ€ x , f x â‰¤ (liminf f <| ð“ x)) â†” âˆ€ (r : EReal), IsClosed {x | f x â‰¤ r} :=
  âŸ¨fun H => Closed.isClosed_sublevel (LowerSemicontinuous.Closed H),
    fun a _ => lowerSemicontinuous_of_isClosed_sublevel a _âŸ©

theorem ClosedOn.isClosed_sublevel {f : E â†’ EReal} {s : Set E} (hf : ClosedOn f s) :
    âˆ€ (r : EReal), IsClosed {x | x âˆˆ s âˆ§ f x â‰¤ r} :=
  fun _ => IsSeqClosed.isClosed fun â¦ƒ_â¦„ â¦ƒ_â¦„ xns cx =>
    IsClosed.isSeqClosed hf (fun n => xns n) (Tendsto.prod_mk_nhds cx tendsto_const_nhds)

theorem LowerSemicontinuousOn.ClosedOn {f : E â†’ EReal} {s : Set E}
    (hs : IsClosed s) (hf : âˆ€ x âˆˆ s, f x â‰¤ (liminf f <| ð“[s] x)) :
    ClosedOn f s:= by
  apply IsSeqClosed.isClosed
  intro f' âŸ¨x', y'âŸ© hxy cxy
  rw [Prod.tendsto_iff] at cxy
  let x : â„• -> E := fun (n : â„•) => (f' n).1
  have h1 := isSeqClosed_iff_isClosed.mpr hs (fun n => (hxy n).1) cxy.1
  constructor
  Â· exact h1
  obtain cx := tendsto_nhdsWithin_iff.mpr âŸ¨cxy.1, eventually_of_forall (fun n => (hxy n).1)âŸ©
  obtain xley :=
    fun n => Trans.trans (Trans.trans (Eq.refl ((f âˆ˜ x) n)) (hxy n).2) (Eq.refl (f' n).2)
  calc
      f x' â‰¤ liminf f (ð“[s] x') := hf x' h1
      _ â‰¤ liminf (f âˆ˜ x) atTop := by
        rw [liminf_eq, liminf_eq]
        exact sSup_le_sSup
          fun _ fa => (eventually_iff_seq_eventually.mp (mem_setOf.mp fa)) x cx
      _ â‰¤ liminf (fun (n : â„•) => (f' n).2) atTop := liminf_le_liminf (eventually_of_forall xley)
      _ = y' := (cxy.2).liminf_eq

theorem lowerSemicontinuousOn_of_isClosed_sublevel {f : E â†’ EReal} {s : Set E}
  (h : âˆ€ r : EReal, IsClosed {x | x âˆˆ s âˆ§ f x â‰¤ r}) :
    âˆ€ x âˆˆ s, f x â‰¤ (liminf f <| ð“[s] x) := by
      by_contra h; push_neg at h
      obtain âŸ¨x, âŸ¨_, hxâŸ©âŸ© := h
      obtain âŸ¨t, âŸ¨ltt, tltâŸ©âŸ© := exists_between hx
      have : x âˆˆ {x | x âˆˆ s âˆ§ f x â‰¤ t} := by
        apply isClosed_iff_frequently.mp (h t) x
        apply frequently_iff.mpr; intro _ hu
        have h : âˆƒá¶  (y : E) in ð“[s] x, f y â‰¤ t := by
          apply frequently_iff.mpr; intro _ hu
          obtain âŸ¨x, xu, fxâŸ© :=
            (frequently_iff.mp (frequently_lt_of_liminf_lt (by isBoundedDefault) ltt)) hu
          exact Exists.intro x âŸ¨xu, LT.lt.le fxâŸ©
        obtain âŸ¨x, xu, fx, xsâŸ© := (frequently_iff.mp (frequently_nhdsWithin_iff.mp h)) hu
        exact Exists.intro x âŸ¨xu, âŸ¨xs, fxâŸ©âŸ©
      apply not_le_of_gt tlt this.2

theorem closedOn_iff_IsClosed_sublevel {f : E â†’ EReal} {s : Set E} (hs : IsClosed s):
    ClosedOn f s â†” âˆ€ (r : EReal), IsClosed {x | x âˆˆ s âˆ§ f x â‰¤ r} :=
  âŸ¨fun h _ => ClosedOn.isClosed_sublevel h _,
    fun h => LowerSemicontinuousOn.ClosedOn hs
      fun x xs => lowerSemicontinuousOn_of_isClosed_sublevel h x xsâŸ©

theorem closedOn_iff_LowerSemicontinuousOn {f : E â†’ EReal} {s : Set E} (hs : IsClosed s):
    ClosedOn f s â†” âˆ€ x âˆˆ s, f x â‰¤ (liminf f <| ð“[s] x) :=
  âŸ¨fun H => lowerSemicontinuousOn_of_isClosed_sublevel fun _ => ClosedOn.isClosed_sublevel H _,
    fun h => LowerSemicontinuousOn.ClosedOn hs hâŸ©

theorem lowerSemicontinuousOn_iff_IsClosed_sublevel {f : E â†’ EReal} {s : Set E} (hs : IsClosed s) : 
    (âˆ€ x âˆˆ s, f x â‰¤ (liminf f <| ð“[s] x)) â†” âˆ€ (r : EReal), IsClosed {x | x âˆˆ s âˆ§ f x â‰¤ r} :=
  âŸ¨fun H => ClosedOn.isClosed_sublevel (LowerSemicontinuousOn.ClosedOn hs H),
    fun a _ => lowerSemicontinuousOn_of_isClosed_sublevel a _âŸ©

/-- If `f : E â†’ EReal` is continuous and `s` is closed, then `f` is closed on `s`. -/ 
theorem ContinuousOn.isClosedFun {f : E â†’ EReal} {s : Set E}
    (hs : IsClosed s) (hf : ContinuousOn f s) :
  ClosedOn f s := IsClosed.epigraph hs hf