/-
Copyright (c) 2023 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.FieldTheory.Separable
import Mathlib.FieldTheory.SplittingField.Construction

/-!

# Perfect fields and rings

In this file we define perfect fields, together with a generalisation to (commutative) rings in
prime characteristic.

## Main definitions / statements:
 * `PerfectRing`: a ring of characteristic `p` (prime) is said to be perfect in the sense of Serre,
   if its absolute Frobenius map `x ‚Ü¶ x·µñ` is bijective.
 * `PerfectField`: a field `K` is said to be perfect if every irreducible polynomial over `K` is
   separable.
 * `PerfectRing.toPerfectField`: a field that is perfect in the sense of Serre is a perfect field.
 * `PerfectField.toPerfectRing`: a perfect field of characteristic `p` (prime) is perfect in the
   sense of Serre.
 * `PerfectField.ofCharZero`: all fields of characteristic zero are perfect.
 * `PerfectField.ofFinite`: all finite fields are perfect.

-/

open Function Polynomial

/-- A perfect ring of characteristic `p` (prime) in the sense of Serre.

NB: This is not related to the concept with the same name introduced by Bass (related to projective
covers of modules). -/
class PerfectRing (R : Type _) (p : ‚Ñï) [CommSemiring R] [Fact p.Prime] [CharP R p] : Prop where
  /-- A ring is perfect if the Frobenius map is bijective. -/
  bijective_frobenius : Bijective $ frobenius R p

section PerfectRing

variable (R : Type _) (p : ‚Ñï) [CommSemiring R] [Fact p.Prime] [CharP R p]

/-- For a reduced ring, surjectivity of the Frobenius map is a sufficient condition for perfection.
-/
lemma PerfectRing.ofSurjective (R : Type _) (p : ‚Ñï) [CommRing R] [Fact p.Prime] [CharP R p]
    [IsReduced R] (h : Surjective $ frobenius R p) : PerfectRing R p :=
  ‚ü®frobenius_inj R p, h‚ü©
#align perfect_ring.of_surjective PerfectRing.ofSurjective

instance PerfectRing.ofFiniteOfIsReduced (R : Type _) [CommRing R] [CharP R p]
    [Finite R] [IsReduced R] : PerfectRing R p :=
  ofSurjective _ _ $ Finite.surjective_of_injective (frobenius_inj R p)

variable [PerfectRing R p]

@[simp]
theorem bijective_frobenius : Bijective (frobenius R p) := PerfectRing.bijective_frobenius

@[simp]
theorem injective_frobenius : Injective (frobenius R p) := (bijective_frobenius R p).1

@[simp]
theorem surjective_frobenius : Surjective (frobenius R p) := (bijective_frobenius R p).2

/-- The Frobenius automorphism for a perfect ring. -/
@[simps! apply]
noncomputable def frobeniusEquiv : R ‚âÉ+* R :=
  RingEquiv.ofBijective (frobenius R p) PerfectRing.bijective_frobenius
#align frobenius_equiv frobeniusEquiv

@[simp]
theorem coe_frobeniusEquiv : ‚áë(frobeniusEquiv R p) = frobenius R p := rfl
#align coe_frobenius_equiv coe_frobeniusEquiv

@[simp]
theorem frobeniusEquiv_symm_apply_frobenius (x : R) :
    (frobeniusEquiv R p).symm (frobenius R p x) = x :=
  leftInverse_surjInv PerfectRing.bijective_frobenius x

@[simp]
theorem frobenius_apply_frobeniusEquiv_symm (x : R) :
    frobenius R p ((frobeniusEquiv R p).symm x) = x :=
  surjInv_eq _ _

@[simp]
theorem frobenius_comp_frobeniusEquiv_symm :
    (frobenius R p).comp (frobeniusEquiv R p).symm = RingHom.id R := by
  ext; simp
  -- ‚ä¢ ‚Üë(RingHom.comp (frobenius R p) ‚Üë(RingEquiv.symm (frobeniusEquiv R p))) x‚úù =  ‚Ä¶
       -- üéâ no goals

@[simp]
theorem frobeniusEquiv_symm_comp_frobenius :
    ((frobeniusEquiv R p).symm : R ‚Üí+* R).comp (frobenius R p) = RingHom.id R := by
  ext; simp
  -- ‚ä¢ ‚Üë(RingHom.comp (‚Üë(RingEquiv.symm (frobeniusEquiv R p))) (frobenius R p)) x‚úù  ‚Ä¶
       -- üéâ no goals

@[simp]
theorem frobeniusEquiv_symm_pow_p (x : R) : ((frobeniusEquiv R p).symm x) ^ p = x :=
  frobenius_apply_frobeniusEquiv_symm R p x

theorem injective_pow_p {x y : R} (h : x ^ p = y ^ p) : x = y := (frobeniusEquiv R p).injective h
#align injective_pow_p injective_pow_p

lemma polynomial_expand_eq (f : R[X]) :
    expand R p f = (f.map (frobeniusEquiv R p).symm) ^ p := by
  rw [‚Üê (f.map (S := R) (frobeniusEquiv R p).symm).expand_char p, map_expand, map_map,
    frobenius_comp_frobeniusEquiv_symm, map_id]

@[simp]
theorem not_irreducible_expand (f : R[X]) : ¬¨ Irreducible (expand R p f) := by
  have hp : Fact p.Prime := inferInstance
  -- ‚ä¢ ¬¨Irreducible (‚Üë(expand R p) f)
  rw [polynomial_expand_eq]
  -- ‚ä¢ ¬¨Irreducible (map (‚Üë(RingEquiv.symm (frobeniusEquiv R p))) f ^ p)
  exact fun hf ‚Ü¶ hf.not_unit $ (of_irreducible_pow hp.out.ne_one hf).pow p
  -- üéâ no goals

instance (S : Type _) [CommSemiring S] [CharP S p] [PerfectRing S p] :
    PerfectRing (R √ó S) p := by
  constructor
  -- ‚ä¢ Bijective ‚Üë(frobenius (R √ó S) p)
  have : frobenius (R √ó S) p = Prod.map (frobenius R p) (frobenius S p) := by
    ext <;> simp [frobenius_def]
  rw [this]
  -- ‚ä¢ Bijective (Prod.map ‚Üë(frobenius R p) ‚Üë(frobenius S p))
  exact Bijective.Prod_map (bijective_frobenius R p) (bijective_frobenius S p)
  -- üéâ no goals

end PerfectRing

/-- A perfect field.

See also `PerfectRing` for a generalisation in positive characteristic. -/
class PerfectField (K : Type _) [Field K] : Prop where
  /-- A field is perfect if every irreducible polynomial is separable. -/
  separable_of_irreducible : ‚àÄ {f : K[X]}, Irreducible f ‚Üí f.Separable

lemma PerfectRing.toPerfectField (K : Type _) (p : ‚Ñï)
    [Field K] [hp : Fact p.Prime] [CharP K p] [PerfectRing K p] : PerfectField K := by
  refine' PerfectField.mk $ fun hf ‚Ü¶ _
  -- ‚ä¢ Separable f‚úù
  rcases separable_or p hf with h | ‚ü®-, g, -, rfl‚ü©
  -- ‚ä¢ Separable f‚úù
  ¬∑ assumption
    -- üéâ no goals
  ¬∑ exfalso; revert hf; simp
    -- ‚ä¢ False
             -- ‚ä¢ Irreducible (‚Üë(expand K p) g) ‚Üí False
                        -- üéâ no goals

namespace PerfectField

variable (K : Type _) [Field K]

instance ofCharZero [CharZero K] : PerfectField K := ‚ü®Irreducible.separable‚ü©

instance ofFinite [Finite K] : PerfectField K := by
  obtain ‚ü®p, _instP‚ü© := CharP.exists K
  -- ‚ä¢ PerfectField K
  have : Fact p.Prime := ‚ü®CharP.char_is_prime K p‚ü©
  -- ‚ä¢ PerfectField K
  exact PerfectRing.toPerfectField K p
  -- üéâ no goals

variable [PerfectField K]

/-- A perfect field of characteristic `p` (prime) is a perfect ring. -/
instance toPerfectRing (p : ‚Ñï) [hp : Fact p.Prime] [CharP K p] : PerfectRing K p := by
  refine' PerfectRing.ofSurjective _ _ $ fun y ‚Ü¶ _
  -- ‚ä¢ ‚àÉ a, ‚Üë(frobenius K p) a = y
  let f : K[X] := X ^ p - C y
  -- ‚ä¢ ‚àÉ a, ‚Üë(frobenius K p) a = y
  let L := f.SplittingField
  -- ‚ä¢ ‚àÉ a, ‚Üë(frobenius K p) a = y
  let Œπ := algebraMap K L
  -- ‚ä¢ ‚àÉ a, ‚Üë(frobenius K p) a = y
  have hf_deg : f.degree ‚â† 0 := by
    rw [degree_X_pow_sub_C hp.out.pos y, p.cast_ne_zero]; exact hp.out.ne_zero
  let a : L := f.rootOfSplits Œπ (SplittingField.splits f) hf_deg
  -- ‚ä¢ ‚àÉ a, ‚Üë(frobenius K p) a = y
  have hfa : aeval a f = 0 := by rw [aeval_def, map_rootOfSplits _ (SplittingField.splits f) hf_deg]
  -- ‚ä¢ ‚àÉ a, ‚Üë(frobenius K p) a = y
  have ha_pow : a ^ p = Œπ y := by rwa [AlgHom.map_sub, aeval_X_pow, aeval_C, sub_eq_zero] at hfa
  -- ‚ä¢ ‚àÉ a, ‚Üë(frobenius K p) a = y
  let g : K[X] := minpoly K a
  -- ‚ä¢ ‚àÉ a, ‚Üë(frobenius K p) a = y
  suffices : (g.map Œπ).natDegree = 1
  -- ‚ä¢ ‚àÉ a, ‚Üë(frobenius K p) a = y
  ¬∑ rw [g.natDegree_map, ‚Üê degree_eq_iff_natDegree_eq_of_pos Nat.one_pos] at this
    -- ‚ä¢ ‚àÉ a, ‚Üë(frobenius K p) a = y
    obtain ‚ü®a' : K, ha' : Œπ a' = a‚ü© := minpoly.mem_range_of_degree_eq_one K a this
    -- ‚ä¢ ‚àÉ a, ‚Üë(frobenius K p) a = y
    refine' ‚ü®a', NoZeroSMulDivisors.algebraMap_injective K L _‚ü©
    -- ‚ä¢ ‚Üë(algebraMap K L) (‚Üë(frobenius K p) a') = ‚Üë(algebraMap K L) y
    rw [RingHom.map_frobenius, ha', frobenius_def, ha_pow]
    -- üéâ no goals
  have hg_dvd : g.map Œπ ‚à£ (X - C a) ^ p := by
    convert Polynomial.map_dvd Œπ (minpoly.dvd K a hfa)
    rw [sub_pow_char, Polynomial.map_sub, Polynomial.map_pow, map_X, map_C, ‚Üê ha_pow, map_pow]
  have ha : IsIntegral K a := isIntegral_of_finite K a
  -- ‚ä¢ natDegree (map Œπ g) = 1
  have hg_pow : g.map Œπ = (X - C a) ^ (g.map Œπ).natDegree := by
    obtain ‚ü®q, -, hq‚ü© := (dvd_prime_pow (prime_X_sub_C a) p).mp hg_dvd
    rw [eq_of_monic_of_associated ((minpoly.monic ha).map Œπ) ((monic_X_sub_C a).pow q) hq,
      natDegree_pow, natDegree_X_sub_C, mul_one]
  have hg_sep : (g.map Œπ).Separable := (separable_of_irreducible $ minpoly.irreducible ha).map
  -- ‚ä¢ natDegree (map Œπ g) = 1
  rw [hg_pow] at hg_sep
  -- ‚ä¢ natDegree (map Œπ g) = 1
  refine' (Separable.of_pow (not_isUnit_X_sub_C a) _ hg_sep).2
  -- ‚ä¢ natDegree (map Œπ g) ‚â† 0
  rw [g.natDegree_map Œπ, ‚Üê Nat.pos_iff_ne_zero, natDegree_pos_iff_degree_pos]
  -- ‚ä¢ 0 < degree g
  exact minpoly.degree_pos ha
  -- üéâ no goals

end PerfectField
