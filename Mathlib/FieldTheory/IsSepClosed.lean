/-
Copyright (c) 2023 Jz Pan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jz Pan
-/
import Mathlib.FieldTheory.IsAlgClosed.AlgebraicClosure

/-!
# Separably Closed Field

In this file we define the typeclass for separably closed fields and separable closures,
and prove some of their properties.

## Main Definitions

- `IsSepClosed k` is the typeclass saying `k` is a separably closed field, i.e. every separable
  polynomial in `k` splits.

- `IsSepClosure k K` is the typeclass saying `K` is a separable closure of `k`, where `k` is a
  field. This means that `K` is separably closed and separable over `k`.

## Tags

separable closure, separably closed

## TODO

- `IsSepClosed.lift` is a map from a separable extension `L` of `k`, into any separably
  closed extension of `k`.

- `IsSepClosed.equiv` is a proof that any two separable closures of the
  same field are isomorphic.

- If `K` is a separably closed field (or algebraically closed field) containing `k`, then all
  elements of `K` which are separable over `k` form a separable closure of `k`.

- Using the above result, construct a separable closure as a subfield of an algebraic closure.

- If `k` is a perfect field, then its separable closure coincides with its algebraic closure.

- An algebraic extension of a separably closed field is purely inseparable.

- Maximal separable subextension ...

-/

universe u v w

open scoped Classical BigOperators Polynomial

open Polynomial

variable (k : Type u) [Field k] (K : Type v) [Field K]

/-- Typeclass for separably closed fields.

To show `Polynomial.Splits p f` for an arbitrary ring homomorphism `f`,
see `IsSepClosed.splits_codomain` and `IsSepClosed.splits_domain`.
-/
class IsSepClosed : Prop where
  splits_of_separable : âˆ€ p : k[X], p.Separable â†’ (p.Splits <| RingHom.id k)

variable {k} {K}

/-- Every separable polynomial splits in the field extension `f : k â†’+* K` if `K` is
separably closed.

See also `IsSepClosed.splits_domain` for the case where `k` is separably closed.
-/
theorem IsSepClosed.splits_codomain [IsSepClosed K] {f : k â†’+* K}
    (p : k[X]) (h : p.Separable) : p.Splits f := by
  convert IsSepClosed.splits_of_separable (p.map f) (Separable.map h); simp [splits_map_iff]
  -- âŠ¢ Splits f p â†” Splits (RingHom.id K) (map f p)
                                                                       -- ğŸ‰ no goals

/-- Every separable polynomial splits in the field extension `f : k â†’+* K` if `k` is
separably closed.

See also `IsSepClosed.splits_codomain` for the case where `k` is separably closed.
-/
theorem IsSepClosed.splits_domain [IsSepClosed k] {f : k â†’+* K}
    (p : k[X]) (h : p.Separable) : p.Splits f :=
  Polynomial.splits_of_splits_id _ <| IsSepClosed.splits_of_separable _ h

namespace IsSepClosed

theorem exists_root [IsSepClosed k] (p : k[X]) (hp : p.degree â‰  0) (hsep : p.Separable) :
    âˆƒ x, IsRoot p x :=
  exists_root_of_splits _ (IsSepClosed.splits_of_separable p hsep) hp

theorem exists_pow_nat_eq [IsSepClosed k] (x : k) (n : â„•) [hn : NeZero (n : k)] :
    âˆƒ z, z ^ n = x := by
  have hn' : 0 < n := Nat.pos_of_ne_zero <| fun h => by
    rw [h, Nat.cast_zero] at hn
    exact hn.out rfl
  have : degree (X ^ n - C x) â‰  0 := by
    rw [degree_X_pow_sub_C hn' x]
    exact (WithBot.coe_lt_coe.2 hn').ne'
  by_cases hx : x = 0
  -- âŠ¢ âˆƒ z, z ^ n = x
  Â· exact âŸ¨0, by rw [hx, pow_eq_zero_iff hn']âŸ©
    -- ğŸ‰ no goals
  Â· obtain âŸ¨z, hzâŸ© := exists_root _ this <| separable_X_pow_sub_C x hn.out hx
    -- âŠ¢ âˆƒ z, z ^ n = x
    use z
    -- âŠ¢ z ^ n = x
    simpa [eval_C, eval_X, eval_pow, eval_sub, IsRoot.def, sub_eq_zero] using hz
    -- ğŸ‰ no goals

theorem exists_eq_mul_self [IsSepClosed k] (x : k) [h2 : NeZero (2 : k)] : âˆƒ z, x = z * z := by
  rcases exists_pow_nat_eq x 2 with âŸ¨z, rflâŸ©
  -- âŠ¢ âˆƒ z_1, z ^ 2 = z_1 * z_1
  exact âŸ¨z, sq zâŸ©
  -- ğŸ‰ no goals

theorem roots_eq_zero_iff [IsSepClosed k] {p : k[X]} (hsep : p.Separable) :
    p.roots = 0 â†” p = Polynomial.C (p.coeff 0) := by
  refine' âŸ¨fun h => _, fun hp => by rw [hp, roots_C]âŸ©
  -- âŠ¢ p = â†‘C (coeff p 0)
  cases' le_or_lt (degree p) 0 with hd hd
  -- âŠ¢ p = â†‘C (coeff p 0)
  Â· exact eq_C_of_degree_le_zero hd
    -- ğŸ‰ no goals
  Â· obtain âŸ¨z, hzâŸ© := IsSepClosed.exists_root p hd.ne' hsep
    -- âŠ¢ p = â†‘C (coeff p 0)
    rw [â† mem_roots (ne_zero_of_degree_gt hd), h] at hz
    -- âŠ¢ p = â†‘C (coeff p 0)
    simp at hz
    -- ğŸ‰ no goals

theorem exists_evalâ‚‚_eq_zero [IsSepClosed K] (f : k â†’+* K)
    (p : k[X]) (hp : p.degree â‰  0) (hsep : p.Separable) :
    âˆƒ x, p.evalâ‚‚ f x = 0 :=
  let âŸ¨x, hxâŸ© := exists_root (p.map f) (by rwa [degree_map_eq_of_injective f.injective])
                                           -- ğŸ‰ no goals
    (Separable.map hsep)
  âŸ¨x, by rwa [evalâ‚‚_eq_eval_map, â† IsRoot]âŸ©
         -- ğŸ‰ no goals

variable (k)

theorem exists_aeval_eq_zero [IsSepClosed K] [Algebra k K] (p : k[X])
    (hp : p.degree â‰  0) (hsep : p.Separable) : âˆƒ x : K, aeval x p = 0 :=
  exists_evalâ‚‚_eq_zero (algebraMap k K) p hp hsep

theorem of_exists_root (H : âˆ€ p : k[X], p.Monic â†’ Irreducible p â†’ Separable p â†’ âˆƒ x, p.eval x = 0) :
    IsSepClosed k := by
  refine âŸ¨fun p hsep â†¦ Or.inr ?_âŸ©
  -- âŠ¢ âˆ€ {g : k[X]}, Irreducible g â†’ g âˆ£ map (RingHom.id k) p â†’ degree g = 1
  intro q hq hdvd
  -- âŠ¢ degree q = 1
  simp only [map_id] at hdvd
  -- âŠ¢ degree q = 1
  have hlc : IsUnit (leadingCoeff q)â»Â¹ := IsUnit.inv <| Ne.isUnit <|
    leadingCoeff_ne_zero.2 <| Irreducible.ne_zero hq
  have hsep' : Separable (q * C (leadingCoeff q)â»Â¹) :=
    Separable.mul (Separable.of_dvd hsep hdvd) ((separable_C _).2 hlc)
    (by simpa only [â† isCoprime_mul_unit_right_right (isUnit_C.2 hlc) q 1, one_mul]
      using isCoprime_one_right (x := q))
  have hirr' := hq
  -- âŠ¢ degree q = 1
  rw [â† irreducible_mul_isUnit (isUnit_C.2 hlc)] at hirr'
  -- âŠ¢ degree q = 1
  obtain âŸ¨x, hxâŸ© := H (q * C (leadingCoeff q)â»Â¹) (monic_mul_leadingCoeff_inv hq.ne_zero) hirr' hsep'
  -- âŠ¢ degree q = 1
  exact degree_mul_leadingCoeff_inv q hq.ne_zero â–¸ degree_eq_one_of_irreducible_of_root hirr' hx
  -- ğŸ‰ no goals

theorem degree_eq_one_of_irreducible [IsSepClosed k] {p : k[X]}
    (hp : Irreducible p) (hsep : p.Separable) : p.degree = 1 :=
  degree_eq_one_of_irreducible_of_splits hp (IsSepClosed.splits_codomain p hsep)

variable {k}

theorem algebraMap_surjective
    [IsSepClosed k] [Algebra k K] [IsSeparable k K] :
    Function.Surjective (algebraMap k K) := by
  refine fun x => âŸ¨-(minpoly k x).coeff 0, ?_âŸ©
  -- âŠ¢ â†‘(algebraMap k K) (-coeff (minpoly k x) 0) = x
  have hq : (minpoly k x).leadingCoeff = 1 := minpoly.monic (IsSeparable.isIntegral k x)
  -- âŠ¢ â†‘(algebraMap k K) (-coeff (minpoly k x) 0) = x
  have hsep : (minpoly k x).Separable := IsSeparable.separable k x
  -- âŠ¢ â†‘(algebraMap k K) (-coeff (minpoly k x) 0) = x
  have h : (minpoly k x).degree = 1 :=
    degree_eq_one_of_irreducible k (minpoly.irreducible (IsSeparable.isIntegral k x)) hsep
  have : aeval x (minpoly k x) = 0 := minpoly.aeval k x
  -- âŠ¢ â†‘(algebraMap k K) (-coeff (minpoly k x) 0) = x
  rw [eq_X_add_C_of_degree_eq_one h, hq, C_1, one_mul, aeval_add, aeval_X, aeval_C,
    add_eq_zero_iff_eq_neg] at this
  exact (RingHom.map_neg (algebraMap k K) ((minpoly k x).coeff 0)).symm â–¸ this.symm
  -- ğŸ‰ no goals

end IsSepClosed

variable (k) (K)

/-- Typeclass for an extension being a separable closure. -/
class IsSepClosure [Algebra k K] : Prop where
  sep_closed : IsSepClosed K
  separable : IsSeparable k K

variable {k} {K}

theorem isSepClosure_iff [Algebra k K] :
    IsSepClosure k K â†” IsSepClosed K âˆ§ IsSeparable k K :=
  âŸ¨fun h => âŸ¨h.1, h.2âŸ©, fun h => âŸ¨h.1, h.2âŸ©âŸ©

instance (priority := 100) IsSepClosure.normal [Algebra k K]
    [IsSepClosure k K] : Normal k K :=
  âŸ¨fun x => by apply IsIntegral.isAlgebraic; exact IsSepClosure.separable.isIntegral' x,
               -- âŠ¢ IsIntegral k x
                                             -- ğŸ‰ no goals
    fun x => @IsSepClosed.splits_codomain _ _ _ _ (IsSepClosure.sep_closed k) _ _ (by
      have : IsSeparable k K := IsSepClosure.separable
      -- âŠ¢ Separable (minpoly k x)
      exact IsSeparable.separable k x)âŸ©
      -- ğŸ‰ no goals
