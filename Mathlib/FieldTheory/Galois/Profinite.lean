/-
Copyright (c) 2024 Nailin Guan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nailin Guan, Yuyang Zhao, Jujian Zhang
-/

import Mathlib.FieldTheory.KrullTopology
import Mathlib.FieldTheory.Galois.GaloisClosure
import Mathlib.Topology.Algebra.Category.ProfiniteGrp.Basic

/-!

# Galois Group as a profinite group

In this file, we prove that in a field extension `K/k`, there is a continuous isomorphism between
`Gal(K/k)` and the limit of `Gal(L/k)`, where `L` is a finite galois intermediate field ordered by
inverse inclusion, thus making `Gal(K/k)` profinite because the limit is profinite.

# Main definitions and results

In a field extension `K/k`

* `finGaloisGroup L` : The (finite) Galois group `Gal(L/k)` associated to a
  `L : FiniteGaloisIntermediateField k K` `L`.

* `finGaloisGroupMap` : For `FiniteGaloisIntermediateField` s `L‚ÇÅ` and `L‚ÇÇ` with `L‚ÇÇ ‚â§ L‚ÇÅ`
  giving the restriction of `Gal(L‚ÇÅ/k)` to `Gal(L‚ÇÇ/k)`

* `finGaloisGroupFunctor` : Mapping `FiniteGaloisIntermediateField` ordered by inverse inclusion to
  its corresponding Galois Group as `FiniteGrp`.

* `finGaloisGroup L` : The (finite) Galois group `Gal(L/k)` associated to a
  `L : FiniteGaloisIntermediateField k K` `L`.

* `finGaloisGroupMap` : For `FiniteGaloisIntermediateField` s `L‚ÇÅ` and `L‚ÇÇ` with `L‚ÇÇ ‚â§ L‚ÇÅ`
  giving the restriction of `Gal(L‚ÇÅ/k)` to `Gal(L‚ÇÇ/k)`

* `finGaloisGroupFunctor` : Mapping `FiniteGaloisIntermediateField` ordered by inverse inclusion to
  its corresponding Galois Group as `FiniteGrp`.

* `HomtoLimit` : Based on the canonical projection from `Gal(K/k)` to any `Gal(L/k)`
  where `L` is a `FiniteGaloisIntermediateField`, it can be easily verified that
  the projections are compatible with the morphisms on `FiniteGaloisIntermediateField`
  (ordered by inverse inclusion)

* `ContinuousMulEquiv` : A ContinuousMulEquiv from `Gal(K/k)` to `lim Gal(L/k)`
  where `L` is `FiniteGaloisIntermediateField`, ordered by inverse inclusion.
  The proof consists of three main parts:
  1. Injectivity :
    Notice that the coordinate at the normal closure of simple extension of `x`
    is different for two element of `Gal(K/k)` mapping `x` differently.
  2. Surjectivity :
    A lemma is needed (lift): for an element `g` in `lim Gal(L/k)` and any two
    `FiniteGaloisIntermediateField` `L‚ÇÅ L‚ÇÇ` containing an element`x`,
    `g` in the coordinate of `L‚ÇÅ` and `L‚ÇÇ` maps `x` to the same element of `K`.
    Then by defining the image of `g` in `Gal(K/k)` pointwise in `K` and use the lemma repeatedly,
    we can get an AlgHom. With the bijectivity of the AlgHom in an algebraic extension, it can be
    made into an element of `Gal(K/k)`
  3. Two-sided continuity : Notice that `Gal(K/k)` is `T2`,
    `lim Gal(L/k)` ordered by inverse inclusion is Profinite thus compact, we only need
    continuity from `lim Gal(L/k)` to `Gal(K/k)`, which only needs continuity at `1`.
    It can be easily verified by checking the preimage of GroupFilterBasis is open.

* `Profinite` Ôºö`Gal(K/k)` is continuously isomorphic (as a topological group) to `lim Gal(L/k)`
  where `L` is `FiniteGaloisIntermediateField`, ordered by inverse inclusion, thus `Profinite`

-/

open CategoryTheory Opposite

variable {k K : Type*} [Field k] [Field K] [Algebra k K]

section Profinite

/-- The (finite) Galois group `Gal(L / k)` associated to a
`L : FiniteGaloisIntermediateField k K` `L`. -/
def FiniteGaloisIntermediateField.finGaloisGroup (L : FiniteGaloisIntermediateField k K) :
    FiniteGrp :=
  letI := AlgEquiv.fintype k L
  FiniteGrp.of <| L ‚âÉ‚Çê[k] L

/-- For `FiniteGaloisIntermediateField` s `L‚ÇÅ` and `L‚ÇÇ` with `L‚ÇÇ ‚â§ L‚ÇÅ`
  the restriction homomorphism from `Gal(L‚ÇÅ/k)` to `Gal(L‚ÇÇ/k)` -/
noncomputable def finGaloisGroupMap {L‚ÇÅ L‚ÇÇ : (FiniteGaloisIntermediateField k K)·µí·µñ}
    (le : L‚ÇÅ ‚ü∂ L‚ÇÇ) : L‚ÇÅ.unop.finGaloisGroup ‚ü∂ L‚ÇÇ.unop.finGaloisGroup :=
  haveI : Normal k L‚ÇÇ.unop := IsGalois.to_normal
  letI : Algebra L‚ÇÇ.unop L‚ÇÅ.unop := RingHom.toAlgebra (Subsemiring.inclusion <| leOfHom le.1)
  haveI : IsScalarTower k L‚ÇÇ.unop L‚ÇÅ.unop := IsScalarTower.of_algebraMap_eq (congrFun rfl)
  FiniteGrp.ofHom (AlgEquiv.restrictNormalHom L‚ÇÇ.unop)

namespace finGaloisGroupMap

@[simp]
lemma map_id (L : (FiniteGaloisIntermediateField k K)·µí·µñ) :
    (finGaloisGroupMap (ùüô L)) = ùüô L.unop.finGaloisGroup :=
  AlgEquiv.restrictNormalHom_id _ _

@[simp]
lemma map_comp {L‚ÇÅ L‚ÇÇ L‚ÇÉ : (FiniteGaloisIntermediateField k K)·µí·µñ} (f : L‚ÇÅ ‚ü∂ L‚ÇÇ) (g : L‚ÇÇ ‚ü∂ L‚ÇÉ) :
    finGaloisGroupMap (f ‚â´ g) = finGaloisGroupMap f ‚â´ finGaloisGroupMap g := by
  iterate 2
    induction L‚ÇÅ with | _ L‚ÇÅ => ?_
    induction L‚ÇÇ with | _ L‚ÇÇ => ?_
    induction L‚ÇÉ with | _ L‚ÇÉ => ?_
  letI : Algebra L‚ÇÉ L‚ÇÇ := RingHom.toAlgebra (Subsemiring.inclusion g.unop.le)
  letI : Algebra L‚ÇÇ L‚ÇÅ := RingHom.toAlgebra (Subsemiring.inclusion f.unop.le)
  letI : Algebra L‚ÇÉ L‚ÇÅ := RingHom.toAlgebra (Subsemiring.inclusion (g.unop.le.trans f.unop.le))
  haveI : IsScalarTower k L‚ÇÇ L‚ÇÅ := IsScalarTower.of_algebraMap_eq' rfl
  haveI : IsScalarTower k L‚ÇÉ L‚ÇÅ := IsScalarTower.of_algebraMap_eq' rfl
  haveI : IsScalarTower k L‚ÇÉ L‚ÇÇ := IsScalarTower.of_algebraMap_eq' rfl
  haveI : IsScalarTower L‚ÇÉ L‚ÇÇ L‚ÇÅ := IsScalarTower.of_algebraMap_eq' rfl
  apply IsScalarTower.AlgEquiv.restrictNormalHom_comp k L‚ÇÉ L‚ÇÇ L‚ÇÅ

end finGaloisGroupMap

variable (k K) in
/-- The functor from `FiniteGaloisIntermediateField` (ordered by reverse inclusion) to `FiniteGrp`,
mapping each intermediate field `K/L/k` to `Gal (L/k)`.-/
noncomputable def finGaloisGroupFunctor : (FiniteGaloisIntermediateField k K)·µí·µñ ‚•§ FiniteGrp where
  obj L := L.unop.finGaloisGroup
  map := finGaloisGroupMap
  map_id := finGaloisGroupMap.map_id
  map_comp := finGaloisGroupMap.map_comp

open FiniteGaloisIntermediateField

variable {k K : Type*} [Field k] [Field K] [Algebra k K]

namespace InfiniteGalois

variable (k K) in
/--`finGaloisGroupFunctor` composite with the forgetful functor from `FiniteGrp` to `ProfiniteGrp`-/
noncomputable abbrev profinGaloisGroupFunctor :
    (FiniteGaloisIntermediateField k K)·µí·µñ ‚•§ ProfiniteGrp :=
    (finGaloisGroupFunctor k K) ‚ãô forget‚ÇÇ FiniteGrp ProfiniteGrp

variable (k K) in
/--Define the homomorphism from `Gal(K/k)` to `lim Gal(L/k)` where `L` is a
  `FiniteGaloisIntermediateField` ordered by inverse inclusion. This homomorphism is given by the
  canonical projection from `Gal(K/k)` to `Gal(L/k)` viewing the limit as a
  subgroup of the product space. -/
@[simps]
noncomputable def homtoLimit : (K ‚âÉ‚Çê[k] K) ‚Üí*
    ProfiniteGrp.limit (profinGaloisGroupFunctor k K) where
  toFun œÉ := {
    val := fun L => (AlgEquiv.restrictNormalHom L.unop) œÉ
    property := fun {L‚ÇÅ L‚ÇÇ} œÄ ‚Ü¶ by
      dsimp [finGaloisGroupFunctor, finGaloisGroupMap]
      letI : Algebra L‚ÇÇ.unop L‚ÇÅ.unop := RingHom.toAlgebra (Subsemiring.inclusion œÄ.1.le)
      letI : IsScalarTower k L‚ÇÇ.unop L‚ÇÅ.unop := IsScalarTower.of_algebraMap_eq (congrFun rfl)
      letI : IsScalarTower L‚ÇÇ.unop L‚ÇÅ.unop K := IsScalarTower.of_algebraMap_eq (congrFun rfl)
      apply (IsScalarTower.AlgEquiv.restrictNormalHom_comp_apply L‚ÇÇ.unop L‚ÇÅ.unop œÉ).symm }
  map_one' := by
    simp only [map_one]
    rfl
  map_mul' x y := by
    simp only [map_mul]
    rfl

/--Define the coordinate map from `lim Gal(L/k)` to a specific `Gal(L/k)`-/
noncomputable def proj (L : FiniteGaloisIntermediateField k K) :
    ProfiniteGrp.limit (profinGaloisGroupFunctor k K) ‚Üí* (L ‚âÉ‚Çê[k] L) where
  toFun g := g.val (op L)
  map_one' := rfl
  map_mul' _ _ := rfl

@[simp]
lemma finGaloisGroupFunctor_proj (g : ProfiniteGrp.limit (profinGaloisGroupFunctor k K))
    {L‚ÇÅ L‚ÇÇ : FiniteGaloisIntermediateField k K} (h : L‚ÇÅ ‚ü∂ L‚ÇÇ) :
    (finGaloisGroupFunctor k K).map h.op (proj L‚ÇÇ g) = proj L‚ÇÅ g :=
  g.prop h.op

lemma proj_lift (L : FiniteGaloisIntermediateField k K)
    (g : ProfiniteGrp.limit (profinGaloisGroupFunctor k K)) (x : L)
    (L' : FiniteGaloisIntermediateField k K) (h : L ‚â§ L') :
    (proj L g x).val = (proj L' g ‚ü®x, h x.2‚ü©).val := by
  induction L with | _ L => ?_
  induction L' with | _ L' => ?_
  letI : Algebra L L' := RingHom.toAlgebra (Subsemiring.inclusion h)
  letI : IsScalarTower k L L' := IsScalarTower.of_algebraMap_eq (congrFun rfl)
  rw [‚Üê finGaloisGroupFunctor_proj g h.hom]
  show (algebraMap L' K (algebraMap L L' (AlgEquiv.restrictNormal (proj (mk L') g) L x))) = _
  rw [AlgEquiv.restrictNormal_commutes (proj (mk L') g) L]
  rfl

lemma proj_lift_adjoin_simple [IsGalois k K]
    (g : ProfiniteGrp.limit (profinGaloisGroupFunctor k K))
    (x : K) (y : adjoin k {x}) (L : FiniteGaloisIntermediateField k K)
    (h : x ‚àà L.toIntermediateField) :
    (proj (adjoin k {x}) g y).val = (proj L g ‚ü®y, adjoin_simple_le_iff.mpr h y.2‚ü©).val :=
  proj_lift _ g y _ _

/--Define a function from `K` to `K` pointwise using a family of compatible elements of
  `Gal(L/k)` where `L` is a `FiniteGaloisIntermediateField`-/
noncomputable def toAlgEquivAux [IsGalois k K]
    (g : ProfiniteGrp.limit (profinGaloisGroupFunctor k K)) : K ‚Üí K :=
  fun x ‚Ü¶ (proj (adjoin k {x}) g ‚ü®x, subset_adjoin _ _ (by simp only [Set.mem_singleton_iff])‚ü©).val

lemma toAlgEquivAux_def [IsGalois k K] (g : ProfiniteGrp.limit (profinGaloisGroupFunctor k K))
    (x : K) (L : FiniteGaloisIntermediateField k K) (hx : x ‚àà L.toIntermediateField) :
    toAlgEquivAux g x = (proj L g ‚ü®x, hx‚ü©).val :=
  proj_lift_adjoin_simple g _ _ L hx

lemma mk_toAlgEquivAux [IsGalois k K] (g : ProfiniteGrp.limit (profinGaloisGroupFunctor k K))
    (x : K) (L : FiniteGaloisIntermediateField k K)
    (hx' : toAlgEquivAux g x ‚àà L.toIntermediateField) (hx : x ‚àà L.toIntermediateField) :
    (‚ü®toAlgEquivAux g x, hx'‚ü© : L.toIntermediateField) = proj L g ‚ü®x, hx‚ü© := by
  rw [Subtype.eq_iff, Subtype.coe_mk, toAlgEquivAux_def]

lemma toAlgEquivAux_eq_liftNormal [IsGalois k K]
    (g : ProfiniteGrp.limit (profinGaloisGroupFunctor k K))
    (x : K) (L : FiniteGaloisIntermediateField k K) (hx : x ‚àà L.toIntermediateField) :
    toAlgEquivAux g x = (proj L g).liftNormal K x := by
  rw [toAlgEquivAux_def g x L hx]
  exact (AlgEquiv.liftNormal_commutes (proj L g) _ ‚ü®x, hx‚ü©).symm

protected lemma AlgEquiv.aut_inv (œï : K ‚âÉ‚Çê[k] K) : œï‚Åª¬π = œï.symm := rfl

instance instSMulMemClass : SMulMemClass (IntermediateField k K) k K where
  smul_mem := fun _ _ hx ‚Ü¶ IntermediateField.smul_mem _ hx

/--Turn `toAlgEquivAux` into an algEquiv by using `proj_lift` repeatedly-/
@[simps]
noncomputable def toAlgEquiv [IsGalois k K]
    (g : ProfiniteGrp.limit (profinGaloisGroupFunctor k K)) : K ‚âÉ‚Çê[k] K where
  toFun := toAlgEquivAux g
  invFun := toAlgEquivAux g‚Åª¬π
  left_inv x := by
    let L := adjoin k {x, toAlgEquivAux g x}
    have hx : x ‚àà L.1 := subset_adjoin _ _ (by simp only [Set.mem_insert_iff,
      Set.mem_singleton_iff, true_or])
    have hx' : toAlgEquivAux g x ‚àà L.1 := subset_adjoin _ _ (by simp only [Set.mem_insert_iff,
      Set.mem_singleton_iff, or_true])
    simp only [toAlgEquivAux_def _ _ L hx', map_inv, AlgEquiv.aut_inv,
      mk_toAlgEquivAux g x L hx' hx, AlgEquiv.symm_apply_apply]
  right_inv x := by
    let L := adjoin k {x, toAlgEquivAux g‚Åª¬π x}
    have hx : x ‚àà L.1 := subset_adjoin _ _ (by simp only [Set.mem_insert_iff,
      Set.mem_singleton_iff, true_or])
    have hx' : toAlgEquivAux g‚Åª¬π x ‚àà L.1 := subset_adjoin _ _ (by simp only [Set.mem_insert_iff,
      Set.mem_singleton_iff, or_true])
    simp only [toAlgEquivAux_def _ _ L hx', mk_toAlgEquivAux g‚Åª¬π x L hx' hx, map_inv,
      AlgEquiv.aut_inv, AlgEquiv.apply_symm_apply]
  map_mul' x y := by
    dsimp
    have hx : x ‚àà (adjoin k {x, y}).1 := subset_adjoin _ _ (by simp only [Set.mem_insert_iff,
      Set.mem_singleton_iff, true_or])
    have hy : y ‚àà (adjoin k {x, y}).1 := subset_adjoin _ _ (by simp only [Set.mem_insert_iff,
      Set.mem_singleton_iff, or_true])
    rw [toAlgEquivAux_eq_liftNormal g x (adjoin k {x, y}) hx,
      toAlgEquivAux_eq_liftNormal g y (adjoin k {x, y}) hy,
      toAlgEquivAux_eq_liftNormal g (x * y) (adjoin k {x, y}) (mul_mem hx hy), map_mul]
  map_add' x y := by
    have hx : x ‚àà (adjoin k {x, y}).1 := subset_adjoin _ _ (by simp only [Set.mem_insert_iff,
      Set.mem_singleton_iff, true_or])
    have hy : y ‚àà (adjoin k {x, y}).1 := subset_adjoin _ _ (by simp only [Set.mem_insert_iff,
      Set.mem_singleton_iff, or_true])
    simp only [toAlgEquivAux_eq_liftNormal g x (adjoin k {x, y}) hx,
      toAlgEquivAux_eq_liftNormal g y (adjoin k {x, y}) hy,
      toAlgEquivAux_eq_liftNormal g (x + y) (adjoin k {x, y}) (add_mem hx hy), map_add]
  commutes' x := by
    simp only [toAlgEquivAux_eq_liftNormal g _ ‚ä• (algebraMap_mem _ x), AlgEquiv.commutes]

variable (k K) in
/--Turn `HomtoLimit` into a mulEquiv-/
noncomputable def mulEquivtoLimit [IsGalois k K] :
    (K ‚âÉ‚Çê[k] K) ‚âÉ* ProfiniteGrp.limit (profinGaloisGroupFunctor k K) where
  toFun := homtoLimit k K
  map_mul' := map_mul _
  invFun := toAlgEquiv
  left_inv := fun f ‚Ü¶ AlgEquiv.ext fun x =>
    AlgEquiv.restrictNormal_commutes f (adjoin k {x}).1 ‚ü®x, _‚ü©
  right_inv := fun g ‚Ü¶ by
    apply Subtype.val_injective
    ext L
    show (toAlgEquiv g).restrictNormal _ = _
    ext x
    have : ((toAlgEquiv g).restrictNormal L.unop) x = (toAlgEquiv g) x.1 := by
      convert AlgEquiv.restrictNormal_commutes (toAlgEquiv g) L.unop x
    simp_rw [this]
    exact proj_lift_adjoin_simple _ _ _ _ x.2

lemma limtoGalContinuous [IsGalois k K] : Continuous (mulEquivtoLimit k K).symm := by
  apply continuous_of_continuousAt_one _ (continuousAt_def.mpr _ )
  simp only [map_one, krullTopology_mem_nhds_one]
  intro H ‚ü®L, _, hO2‚ü©
  set L' : FiniteGaloisIntermediateField k K := mk <| normalClosure k L K
  have lecl := IntermediateField.le_normalClosure L
  have : L'.1.fixingSubgroup ‚â§ L.fixingSubgroup := fun œÉ h => (mem_fixingSubgroup_iff
    (K ‚âÉ‚Çê[k] K)).mpr (fun y hy => ((mem_fixingSubgroup_iff (K ‚âÉ‚Çê[k] K)).mp h) y (lecl hy))
  have le1 : (mulEquivtoLimit k K).symm ‚Åª¬π' L.fixingSubgroup ‚äÜ (mulEquivtoLimit k K).symm ‚Åª¬π' H :=
    fun ‚¶Éa‚¶Ñ => fun b => hO2 b
  have le : (mulEquivtoLimit k K).symm ‚Åª¬π' L'.1.fixingSubgroup ‚äÜ
    (mulEquivtoLimit k K).symm ‚Åª¬π' H := fun ‚¶Éa‚¶Ñ b ‚Ü¶ le1 (this b)
  apply mem_nhds_iff.mpr
  use (mulEquivtoLimit k K).symm ‚Åª¬π' L'.1.fixingSubgroup
  simp only [le, true_and]
  constructor
  ¬∑ have : (mulEquivtoLimit k K).symm ‚Åª¬π' L'.1.fixingSubgroup =
        mulEquivtoLimit k K '' (L'.1.fixingSubgroup : Set (K ‚âÉ‚Çê[k] K)) := by
      ext œÉ
      constructor
      ¬∑ intro h
        use (mulEquivtoLimit k K).symm œÉ
        simp only [h.out , MulEquiv.apply_symm_apply, and_self]
      ¬∑ rintro ‚ü®_, h1, h2‚ü©
        simp only [‚Üê h2, Set.mem_preimage, MulEquiv.symm_apply_apply, h1]
    rw [this]
    let fix1 : Set ((L : (FiniteGaloisIntermediateField k K)·µí·µñ) ‚Üí
      (profinGaloisGroupFunctor k K).obj L) := {x : ((L : (FiniteGaloisIntermediateField k K)·µí·µñ) ‚Üí
      (profinGaloisGroupFunctor k K).obj L) | x (op L') = 1}
    have C : Continuous (fun (x : (L : (FiniteGaloisIntermediateField k K)·µí·µñ) ‚Üí
      (profinGaloisGroupFunctor k K).obj L) ‚Ü¶ x (op L')) :=
      continuous_apply (op L')
    have : mulEquivtoLimit k K '' L'.1.fixingSubgroup = Set.preimage Subtype.val fix1 := by
      ext x
      constructor
      ¬∑ rintro ‚ü®Œ±, hŒ±1, hŒ±2‚ü©
        simp only [Set.mem_preimage,‚Üê hŒ±2, fix1, Set.mem_setOf_eq, mulEquivtoLimit, homtoLimit,
          MonoidHom.coe_mk, OneHom.coe_mk, MulEquiv.coe_mk, Equiv.coe_fn_mk]
        apply AlgEquiv.ext
        intro x
        apply Subtype.val_injective
        rw [AlgEquiv.restrictNormalHom_apply L'.1 Œ± x, AlgEquiv.one_apply]
        exact hŒ±1 x
      ¬∑ intro h
        simp only [Set.mem_preimage] at h
        use (mulEquivtoLimit _ _).symm x
        simp only [SetLike.mem_coe, MulEquiv.apply_symm_apply, and_true]
        apply (mem_fixingSubgroup_iff (K ‚âÉ‚Çê[k] K)).mpr
        intro y hy
        simp only [AlgEquiv.smul_def]
        have fix := h.out
        set Aut := (mulEquivtoLimit _ _).symm x
        have : mulEquivtoLimit _ _ Aut = x := by simp only [Aut, MulEquiv.apply_symm_apply]
        simp only [‚Üê this, mulEquivtoLimit, homtoLimit, MonoidHom.coe_mk, OneHom.coe_mk,
          MulEquiv.coe_mk, Equiv.coe_fn_mk] at fix
        have fix_y : AlgEquiv.restrictNormalHom L' Aut ‚ü®y, hy‚ü© = ‚ü®y, hy‚ü© := by
          simp only [fix, AlgEquiv.one_apply]
        rw [‚Üê AlgEquiv.restrictNormalHom_apply L'.1 Aut ‚ü®y,hy‚ü©, fix_y]
    have op : IsOpen fix1 := C.isOpen_preimage {1} trivial
    exact this ‚ñ∏ (isOpen_induced op)
  ¬∑ simp only [Set.mem_preimage, map_one, Subsemigroup.mem_carrier, Submonoid.mem_toSubsemigroup,
    Subgroup.mem_toSubmonoid]
    exact congrFun rfl

instance [IsGalois k K] : T2Space (K ‚âÉ‚Çê[k] K) := krullTopology_t2

variable (k K)

/--Turn mulEquivtoLimit, viewed as a bijection, into a homeomorphism-/
noncomputable def limtoGalHomeo [IsGalois k K] :
    (ProfiniteGrp.limit (profinGaloisGroupFunctor k K)) ‚âÉ‚Çú (K ‚âÉ‚Çê[k] K) :=
  Continuous.homeoOfEquivCompactToT2 limtoGalContinuous

instance [IsGalois k K] : CompactSpace (K ‚âÉ‚Çê[k] K) :=
  Homeomorph.compactSpace (limtoGalHomeo k K)

instance [IsGalois k K] : TotallyDisconnectedSpace (K ‚âÉ‚Çê[k] K) :=
  Homeomorph.totallyDisconnectedSpace (limtoGalHomeo k K)

/--Turn `mulEquivtoLimit` into a continuousMulEquiv-/
noncomputable def continuousMulEquivtoLimit [IsGalois k K] :
    ContinuousMulEquiv (K ‚âÉ‚Çê[k] K) (ProfiniteGrp.limit (profinGaloisGroupFunctor k K)) where
  toMulEquiv := mulEquivtoLimit k K
  continuous_toFun := (limtoGalHomeo _ _).continuous_invFun
  continuous_invFun := (limtoGalHomeo _ _).continuous_toFun

/--Turn `Gal(K/k)` into a profinite group using the continuousMulEquiv above-/
noncomputable def ProfiniteGalGrp [IsGalois k K] : ProfiniteGrp :=
  ProfiniteGrp.ofContinuousMulEquivProfiniteGrp (continuousMulEquivtoLimit k K).symm

variable {k K} in
theorem restrictNormalHomContinuous (L : IntermediateField k K) [Normal k L] :
    Continuous (AlgEquiv.restrictNormalHom (F := k) (K‚ÇÅ := K) L) := by
  apply continuous_of_continuousAt_one _ (continuousAt_def.mpr _ )
  rw [map_one]
  intro N hN
  rw [krullTopology_mem_nhds_one] at hN
  obtain ‚ü®L', _, hO‚ü© := hN
  letI : FiniteDimensional k L' :=
    Module.Finite.equiv <| AlgEquiv.toLinearEquiv <| IntermediateField.liftAlgEquiv L'
  apply mem_nhds_iff.mpr
  use (IntermediateField.lift L').fixingSubgroup
  constructor
  ¬∑ intro x hx
    apply hO
    simp only [SetLike.mem_coe] at hx ‚ä¢
    rw [IntermediateField.mem_fixingSubgroup_iff] at hx ‚ä¢
    intro y hy
    have := AlgEquiv.restrictNormal_commutes x L y
    dsimp at this
    rw [hx y.1 ((IntermediateField.mem_lift y).mpr hy)] at this
    exact SetLike.coe_eq_coe.mp this
  ¬∑ exact ‚ü®IntermediateField.fixingSubgroup_isOpen (IntermediateField.lift L') , congrFun rfl‚ü©

end InfiniteGalois

end Profinite
