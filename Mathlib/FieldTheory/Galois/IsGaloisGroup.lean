/-
Copyright (c) 2025 Thomas Browning. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Thomas Browning
-/
module

public import Mathlib.FieldTheory.Galois.Infinite
public import Mathlib.NumberTheory.NumberField.Basic
public import Mathlib.RingTheory.Invariant.Basic

/-!
# Predicate for Galois Groups

Given an action of a group `G` on an extension of fields `L/K`, we introduce a predicate
`IsGaloisGroup G K L` saying that `G` acts faithfully on `L` with fixed field `K`. In particular,
we do not assume that `L` is an algebraic extension of `K`.

## Implementation notes

We actually define `IsGaloisGroup G A B` for extensions of rings `B/A`, with the same definition
(faithful action on `B` with fixed ring `A`). This definition turns out to axiomatize a common
setup in algebraic number theory where a Galois group `Gal(L/K)` acts on an extension of subrings
`B/A` (e.g., rings of integers). In particular, there are theorems in algebraic number theory that
naturally assume `[IsGaloisGroup G A B]` and whose statements would otherwise require assuming
`(K L : Type*) [Field K] [Field L] [Algebra K L] [IsGalois K L]` (along with predicates relating
`K` and `L` to the rings `A` and `B`) despite `K` and `L` not appearing in the conclusion.

Unfortunately, this definition of `IsGaloisGroup G A B` for extensions of rings `B/A` is
nonstandard and clashes with other notions such as the √©tale fundamental group. In particular, if
`G` is finite and `A` is integrally closed, then  `IsGaloisGroup G A B` is equivalent to `B/A`
being integral and the fields of fractions `Frac(B)/Frac(A)` being Galois with Galois group `G`
(see `IsGaloisGroup.iff_isFractionRing`), rather than `B/A` being √©tale for instance.

But in the absence of a more suitable name, the utility of the predicate `IsGaloisGroup G A B` for
extensions of rings `B/A` seems to outweigh these terminological issues.
-/

@[expose] public section

section CommRing

variable (G A B : Type*) [Group G] [CommSemiring A] [Semiring B] [Algebra A B]
  [MulSemiringAction G B]

/-- `G` is a Galois group for `L/K` if the action of `G` on `L` is faithful with fixed field `K`.
In particular, we do not assume that `L` is an algebraic extension of `K`.

See the implementation notes in this file for the meaning of this definition in the case of rings.
-/
class IsGaloisGroup where
  faithful : FaithfulSMul G B
  commutes : SMulCommClass G A B
  isInvariant : Algebra.IsInvariant A B G

attribute [instance low] IsGaloisGroup.commutes IsGaloisGroup.isInvariant

end CommRing

section Field

variable (G A B K L : Type*) [Group G] [CommRing A] [CommRing B] [MulSemiringAction G B]
  [Algebra A B] [Field K] [Field L] [Algebra K L] [Algebra A K] [Algebra B L] [Algebra A L]
  [IsFractionRing A K] [IsFractionRing B L] [IsScalarTower A K L] [IsScalarTower A B L]
  [MulSemiringAction G L] [SMulDistribClass G B L]

/-- `IsGaloisGroup` for rings implies `IsGaloisGroup` for their fraction fields. -/
theorem IsGaloisGroup.to_isFractionRing [Finite G] [hGAB : IsGaloisGroup G A B] :
    IsGaloisGroup G K L := by
  have hc (a : A) : (algebraMap K L) (algebraMap A K a) = (algebraMap B L) (algebraMap A B a) := by
    simp_rw [‚Üê IsScalarTower.algebraMap_apply]
  refine ‚ü®‚ü®fun h ‚Ü¶ ?_‚ü©, ‚ü®fun g x y ‚Ü¶ ?_‚ü©, ‚ü®fun x h ‚Ü¶ ?_‚ü©‚ü©
  ¬∑ have := hGAB.faithful
    exact eq_of_smul_eq_smul fun y ‚Ü¶ by simpa [‚Üê algebraMap.coe_smul'] using h (algebraMap B L y)
  ¬∑ obtain ‚ü®a, b, hb, rfl‚ü© := IsFractionRing.div_surjective (A := A) x
    obtain ‚ü®c, d, hd, rfl‚ü© := IsFractionRing.div_surjective (A := B) y
    simp [Algebra.smul_def, smul_mul', smul_div‚ÇÄ', hc, ‚Üê algebraMap.coe_smul']
  ¬∑ have := hGAB.isInvariant.isIntegral
    have : Nontrivial A := (IsFractionRing.nontrivial_iff_nontrivial A K).mpr inferInstance
    have : Nontrivial B := (IsFractionRing.nontrivial_iff_nontrivial B L).mpr inferInstance
    obtain ‚ü®x, y, hy, rfl‚ü© := IsFractionRing.div_surjective (A := B) x
    have hy' : algebraMap B L y ‚â† 0 := by simpa using nonZeroDivisors.ne_zero hy
    obtain ‚ü®b, a, ha, hb‚ü© := (Algebra.IsAlgebraic.isAlgebraic (R := A) y).exists_smul_eq_mul x hy
    rw [mul_comm, Algebra.smul_def, mul_comm] at hb
    replace ha : (algebraMap B L) (algebraMap A B a) ‚â† 0 := by simpa [‚Üê hc]
    have hxy : algebraMap B L x / algebraMap B L y =
      algebraMap B L b / algebraMap B L (algebraMap A B a) := by
      rw [div_eq_div_iff hy' ha, ‚Üê map_mul, hb, map_mul]
    obtain ‚ü®b, rfl‚ü© := hGAB.isInvariant.isInvariant b
      (by simpa [ha, hxy, smul_div‚ÇÄ', ‚Üê algebraMap.coe_smul'] using h)
    use algebraMap A K b / algebraMap A K a
    simp [hc, div_eq_div_iff ha hy', ‚Üê map_mul, ‚Üê map_mul, hb]

/-- If `B` is an integral extension of an integrally closed domain `A`, then `IsGaloisGroup` for
their fraction fields implies `IsGaloisGroup` for these rings. -/
theorem IsGaloisGroup.of_isFractionRing [hGKL : IsGaloisGroup G K L]
    [IsIntegrallyClosed A] [Algebra.IsIntegral A B] : IsGaloisGroup G A B := by
  have hc (a : A) : (algebraMap K L) (algebraMap A K a) = (algebraMap B L) (algebraMap A B a) := by
    simp_rw [‚Üê IsScalarTower.algebraMap_apply]
  refine ‚ü®‚ü®fun h ‚Ü¶ ?_‚ü©, ‚ü®fun g x y ‚Ü¶ IsFractionRing.injective B L ?_‚ü©, ‚ü®fun x h ‚Ü¶ ?_‚ü©‚ü©
  ¬∑ have := hGKL.faithful
    refine eq_of_smul_eq_smul fun (y : L) ‚Ü¶ ?_
    obtain ‚ü®a, b, hb, rfl‚ü© := IsFractionRing.div_surjective (A := B) y
    simp only [smul_div‚ÇÄ', ‚Üê algebraMap.coe_smul', h]
  ¬∑ simp [Algebra.smul_def, algebraMap.coe_smul', ‚Üê hc]
  ¬∑ obtain ‚ü®b, hb‚ü© := hGKL.isInvariant.isInvariant (algebraMap B L x)
      (by simpa [‚Üê algebraMap.coe_smul'])
    have hx : IsIntegral A (algebraMap B L x) := (Algebra.IsIntegral.isIntegral x).algebraMap
    rw [‚Üê hb, isIntegral_algebraMap_iff (algebraMap K L).injective,
      IsIntegrallyClosedIn.isIntegral_iff] at hx
    obtain ‚ü®a, rfl‚ü© := hx
    exact ‚ü®a, by rwa [hc, IsFractionRing.coe_inj] at hb‚ü©

/-- If `G` is finite and `A` is integrally closed then `IsGaloisGroup G A B` is equivalent to `B/A`
being integral and the fields of fractions `Frac(B)/Frac(A)` being Galois with Galois group `G`. -/
theorem IsGaloisGroup.iff_isFractionRing [Finite G] [IsIntegrallyClosed A] :
    IsGaloisGroup G A B ‚Üî Algebra.IsIntegral A B ‚àß IsGaloisGroup G K L :=
  ‚ü®fun h ‚Ü¶ ‚ü®h.isInvariant.isIntegral, h.to_isFractionRing G A B K L‚ü©,
    fun ‚ü®_, h‚ü© ‚Ü¶ h.of_isFractionRing G A B K L‚ü©

attribute [local instance] FractionRing.liftAlgebra in
/--
Assume that `IsGaloisGroup G A B` with `A` and `B` domains, then `G` has a `MulSemiringAction`
on `FractionRing B`. This cannot be an instance since Lean cannot figure out `A`.
-/
noncomputable def FractionRing.mulSemiringAction_of_isGaloisGroup [IsDomain A] [IsDomain B]
    [NoZeroSMulDivisors A B] [IsGaloisGroup G A B] : MulSemiringAction G (FractionRing B) :=
  MulSemiringAction.compHom (FractionRing B)
    ((IsFractionRing.fieldEquivOfAlgEquivHom (FractionRing A) (FractionRing B)).comp
      (MulSemiringAction.toAlgAut G A B))

attribute [local instance] FractionRing.liftAlgebra in
/--
If `G` is finite and `IsGaloisGroup G A B` with `A` and `B` domains, then `G` is also
a Galois group for `FractionRing A / FractionRing B` for the action defined by
`FractionRing.mulSemiringAction_of_isGaloisGroup`.
-/
theorem IsGaloisGroup.toFractionRing [IsDomain A] [IsDomain B] [NoZeroSMulDivisors A B] [Finite G]
    [IsGaloisGroup G A B] :
    letI := FractionRing.mulSemiringAction_of_isGaloisGroup G A B
    IsGaloisGroup G (FractionRing A) (FractionRing B) := by
  letI := FractionRing.mulSemiringAction_of_isGaloisGroup G A B
  have : SMulDistribClass G B (FractionRing B) := ‚ü®fun g b x ‚Ü¶ by
    rw [Algebra.smul_def', Algebra.smul_def', smul_mul']
    congr
    exact IsFractionRing.fieldEquivOfAlgEquiv_algebraMap (FractionRing A) _ _ _ b‚ü©
  apply IsGaloisGroup.to_isFractionRing G A B _ _

open NumberField

instance (K L : Type*) [Field K] [Field L] [NumberField K] [NumberField L] [Algebra K L]
    (G : Type*) [Group G] [MulSemiringAction G L] [IsGaloisGroup G K L] :
    IsGaloisGroup G (ùìû K) (ùìû L) :=
  IsGaloisGroup.of_isFractionRing G (ùìû K) (ùìû L) K L

instance (L : Type*) [Field L] [NumberField L]
    (G : Type*) [Group G] [MulSemiringAction G L] [IsGaloisGroup G ‚Ñö L] :
    IsGaloisGroup G ‚Ñ§ (ùìû L) :=
  IsGaloisGroup.of_isFractionRing G ‚Ñ§ (ùìû L) ‚Ñö L

end Field

variable (G H K L : Type*) [Group G] [Group H] [Field K] [Field L] [Algebra K L]
  [MulSemiringAction G L] [MulSemiringAction H L]

namespace IsGaloisGroup

attribute [instance low] commutes isInvariant

theorem fixedPoints_eq_bot [IsGaloisGroup G K L] :
    FixedPoints.intermediateField G = (‚ä• : IntermediateField K L) := by
  rw [eq_bot_iff]
  exact Algebra.IsInvariant.isInvariant

/-- If `G` is a finite Galois group for `L/K`, then `L/K` is a Galois extension. -/
theorem isGalois [Finite G] [IsGaloisGroup G K L] : IsGalois K L := by
  rw [‚Üê isGalois_iff_isGalois_bot, ‚Üê fixedPoints_eq_bot G]
  exact IsGalois.of_fixed_field L G

/-- If `L/K` is a Galois extension, then `Gal(L/K)` is a Galois group for `L/K`. -/
instance of_isGalois [IsGalois K L] : IsGaloisGroup Gal(L/K) K L where
  faithful := inferInstance
  commutes := inferInstance
  isInvariant := ‚ü®fun x ‚Ü¶ (InfiniteGalois.mem_bot_iff_fixed x).mpr‚ü©

theorem card_eq_finrank [IsGaloisGroup G K L] : Nat.card G = Module.finrank K L := by
  rcases fintypeOrInfinite G with _ | hG
  ¬∑ have : FaithfulSMul G L := faithful K
    rw [‚Üê IntermediateField.finrank_bot', ‚Üê fixedPoints_eq_bot G, Nat.card_eq_fintype_card]
    exact (FixedPoints.finrank_eq_card G L).symm
  ¬∑ rw [Nat.card_eq_zero_of_infinite, eq_comm]
    contrapose! hG
    have : FiniteDimensional K L := FiniteDimensional.of_finrank_pos (Nat.zero_lt_of_ne_zero hG)
    exact Finite.of_injective (MulSemiringAction.toAlgAut G K L)
      (fun _ _ ‚Ü¶ (faithful K).eq_of_smul_eq_smul ‚àò DFunLike.ext_iff.mp)

theorem finiteDimensional [Finite G] [IsGaloisGroup G K L] : FiniteDimensional K L :=
  FiniteDimensional.of_finrank_pos (card_eq_finrank G K L ‚ñ∏ Nat.card_pos)

protected theorem finite [FiniteDimensional K L] [IsGaloisGroup G K L] : Finite G :=
  Nat.finite_of_card_ne_zero (card_eq_finrank G K L ‚ñ∏ Module.finrank_pos.ne')

/-- If `G` is a finite Galois group for `L/K`, then `G` is isomorphic to `Gal(L/K)`. -/
@[simps!] noncomputable def mulEquivAlgEquiv [IsGaloisGroup G K L] [Finite G] : G ‚âÉ* Gal(L/K) :=
  MulEquiv.ofBijective (MulSemiringAction.toAlgAut G K L) (by
    have := isGalois G K L
    have := finiteDimensional G K L
    rw [Nat.bijective_iff_injective_and_card, card_eq_finrank G K L,
      IsGalois.card_aut_eq_finrank K L]
    exact ‚ü®fun _ _ ‚Ü¶ (faithful K).eq_of_smul_eq_smul ‚àò DFunLike.ext_iff.mp, rfl‚ü©)

/-- If `G` and `H` are finite Galois groups for `L/K`, then `G` is isomorphic to `H`. -/
noncomputable def mulEquivCongr [IsGaloisGroup G K L] [Finite G]
    [IsGaloisGroup H K L] [Finite H] : G ‚âÉ* H :=
  (mulEquivAlgEquiv G K L).trans (mulEquivAlgEquiv H K L).symm

@[simp]
theorem mulEquivCongr_apply_smul [IsGaloisGroup G K L] [Finite G] [IsGaloisGroup H K L] [Finite H]
    (g : G) (x : L) : mulEquivCongr G H K L g ‚Ä¢ x = g ‚Ä¢ x :=
  AlgEquiv.ext_iff.mp ((mulEquivAlgEquiv H K L).apply_symm_apply (mulEquivAlgEquiv G K L g)) x

@[simp]
theorem map_mulEquivAlgEquiv_fixingSubgroup
    [IsGaloisGroup G K L] [Finite G] (F : IntermediateField K L) :
    (fixingSubgroup G (F : Set L)).map (mulEquivAlgEquiv G K L) = F.fixingSubgroup := by
  ext g
  obtain ‚ü®g, rfl‚ü© := (mulEquivAlgEquiv G K L).surjective g
  simp [mem_fixingSubgroup_iff]

variable (H H' : Subgroup G) (F F' : IntermediateField K L)

instance subgroup [hGKL : IsGaloisGroup G K L] :
    IsGaloisGroup H (FixedPoints.intermediateField H : IntermediateField K L) L where
  faithful := have := hGKL.faithful; inferInstance
  commutes := inferInstanceAs <| SMulCommClass H (FixedPoints.subfield H L) L
  isInvariant := ‚ü®fun x h ‚Ü¶ ‚ü®‚ü®x, h‚ü©, rfl‚ü©‚ü©

@[simp]
theorem finrank_fixedPoints_eq_card_subgroup [IsGaloisGroup G K L] :
    Module.finrank (FixedPoints.intermediateField H : IntermediateField K L) L = Nat.card H :=
  (card_eq_finrank H (FixedPoints.intermediateField H) L).symm

variable {G K L} in
theorem of_mulEquiv_algEquiv [IsGalois K L] (e : G ‚âÉ* Gal(L/K)) (he : ‚àÄ g x, e g x = g ‚Ä¢ x) :
    IsGaloisGroup G K L where
  faithful := ‚ü®fun {g‚ÇÅ g‚ÇÇ} h ‚Ü¶ e.injective <| AlgEquiv.ext <| by simpa [he]‚ü©
  commutes := ‚ü®by simp [‚Üê he]‚ü©
  isInvariant := ‚ü®fun y hy ‚Ü¶ (InfiniteGalois.mem_bot_iff_fixed y).mpr <|
    e.surjective.forall.mpr <| by simpa [he]‚ü©

instance fixedPoints [Finite G] [FaithfulSMul G L] :
    IsGaloisGroup G (FixedPoints.subfield G L) L :=
  of_mulEquiv_algEquiv (FixedPoints.toAlgAutMulEquiv _ _) fun _ _ ‚Ü¶ rfl

instance intermediateField [Finite G] [hGKL : IsGaloisGroup G K L] :
    IsGaloisGroup (fixingSubgroup G (F : Set L)) F L :=
  let e := ((mulEquivAlgEquiv G K L).subgroupMap (fixingSubgroup G (F : Set L))).trans <|
    (MulEquiv.subgroupCongr (map_mulEquivAlgEquiv_fixingSubgroup ..)).trans <|
    IntermediateField.fixingSubgroupEquiv F
  have := hGKL.isGalois
  .of_mulEquiv_algEquiv e fun _ _ ‚Ü¶ rfl

@[simp]
theorem card_fixingSubgroup_eq_finrank [Finite G] [IsGaloisGroup G K L] :
    Nat.card (fixingSubgroup G (F : Set L)) = Module.finrank F L :=
  card_eq_finrank ..

section GaloisCorrespondence

theorem fixingSubgroup_le_of_le (h : F ‚â§ F') :
    fixingSubgroup G (F' : Set L) ‚â§ fixingSubgroup G (F : Set L) :=
  fun _ hœÉ ‚ü®x, hx‚ü© ‚Ü¶ hœÉ ‚ü®x, h hx‚ü©

section SMulCommClass

variable [SMulCommClass G K L]

@[simp]
theorem fixingSubgroup_bot : fixingSubgroup G ((‚ä• : IntermediateField K L) : Set L) = ‚ä§ := by
  simp [Subgroup.ext_iff, mem_fixingSubgroup_iff, IntermediateField.mem_bot]

@[simp]
theorem fixedPoints_bot :
    (FixedPoints.intermediateField (‚ä• : Subgroup G) : IntermediateField K L) = ‚ä§ := by
  simp [IntermediateField.ext_iff]

theorem le_fixedPoints_iff_le_fixingSubgroup :
    F ‚â§ FixedPoints.intermediateField H ‚Üî H ‚â§ fixingSubgroup G (F : Set L) :=
  ‚ü®fun h g hg x ‚Ü¶ h x.2 ‚ü®g, hg‚ü©, fun h x hx g ‚Ü¶ h g.2 ‚ü®x, hx‚ü©‚ü©

theorem fixedPoints_le_of_le (h : H ‚â§ H') :
    FixedPoints.intermediateField H' ‚â§ (FixedPoints.intermediateField H : IntermediateField K L) :=
  fun _ hœÉ ‚ü®x, hx‚ü© ‚Ü¶ hœÉ ‚ü®x, h hx‚ü©

end SMulCommClass

section IsGaloisGroup

variable [hGKL : IsGaloisGroup G K L]

-- this can't be a simp-lemma since the left-hand side is not in simp normal form
-- and if the theorem was `fixingSubgroup G Set.univ = ‚ä•` then `K` couldn't be inferred
theorem fixingSubgroup_top : fixingSubgroup G ((‚ä§ : IntermediateField K L) : Set L) = ‚ä• := by
  have := hGKL.faithful
  ext; simpa [mem_fixingSubgroup_iff, Set.ext_iff] using MulAction.fixedBy_eq_univ_iff_eq_one

@[simp]
theorem fixedPoints_top :
    (FixedPoints.intermediateField (‚ä§ : Subgroup G) : IntermediateField K L) = ‚ä• := by
  convert IsGaloisGroup.fixedPoints_eq_bot G K L
  ext; simp

/-- The Galois correspondence from intermediate fields to subgroups. -/
noncomputable def intermediateFieldEquivSubgroup [Finite G] :
    IntermediateField K L ‚âÉo (Subgroup G)·µí·µà :=
  have := isGalois G K L
  have := finiteDimensional G K L
  IsGalois.intermediateFieldEquivSubgroup.trans <| (mulEquivAlgEquiv G K L).comapSubgroup.dual

@[simp] theorem intermediateFieldEquivSubgroup_apply [Finite G] {F} :
    intermediateFieldEquivSubgroup G K L F = .toDual (fixingSubgroup G (F : Set L)) := rfl

theorem ofDual_intermediateFieldEquivSubgroup_apply [Finite G] {F} :
    (intermediateFieldEquivSubgroup G K L F).ofDual = fixingSubgroup G (F : Set L) := rfl

@[simp] theorem intermediateFieldEquivSubgroup_symm_apply [Finite G] {H} :
    (intermediateFieldEquivSubgroup G K L).symm H = FixedPoints.intermediateField H.ofDual := by
  obtain ‚ü®H, rfl‚ü© := OrderDual.toDual.surjective H
  simp [IntermediateField.ext_iff, intermediateFieldEquivSubgroup,
    (mulEquivAlgEquiv G K L).surjective.forall, -mulEquivAlgEquiv_symm_apply]

theorem intermediateFieldEquivSubgroup_symm_apply_toDual [Finite G] {H} :
    (intermediateFieldEquivSubgroup G K L).symm (.toDual H) = FixedPoints.intermediateField H :=
  intermediateFieldEquivSubgroup_symm_apply ..

@[simp]
theorem fixingSubgroup_fixedPoints [Finite G] :
    fixingSubgroup G ((FixedPoints.intermediateField H : IntermediateField K L) : Set L) = H := by
  rw [‚Üê intermediateFieldEquivSubgroup_symm_apply_toDual,
    ‚Üê ofDual_intermediateFieldEquivSubgroup_apply,
    OrderIso.apply_symm_apply, OrderDual.ofDual_toDual]

@[simp]
theorem fixedPoints_fixingSubgroup [Finite G] :
    FixedPoints.intermediateField (fixingSubgroup G (F : Set L)) = F := by
  rw [‚Üê ofDual_intermediateFieldEquivSubgroup_apply, ‚Üê intermediateFieldEquivSubgroup_symm_apply,
    OrderIso.symm_apply_apply]

end IsGaloisGroup

end GaloisCorrespondence

end IsGaloisGroup
