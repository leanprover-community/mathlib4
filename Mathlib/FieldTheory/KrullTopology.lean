/-
Copyright (c) 2022 Sebastian Monnet. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sebastian Monnet
-/
import Mathlib.FieldTheory.Galois.Basic
import Mathlib.Topology.Algebra.FilterBasis
import Mathlib.Topology.Algebra.OpenSubgroup
import Mathlib.Tactic.ByContra

/-!
# Krull topology

We define the Krull topology on `L ‚âÉ‚Çê[K] L` for an arbitrary field extension `L/K`. In order to do
this, we first define a `GroupFilterBasis` on `L ‚âÉ‚Çê[K] L`, whose sets are `E.fixingSubgroup` for
all intermediate fields `E` with `E/K` finite dimensional.

## Main Definitions

- `finiteExts K L`. Given a field extension `L/K`, this is the set of intermediate fields that are
  finite-dimensional over `K`.

- `fixedByFinite K L`. Given a field extension `L/K`, `fixedByFinite K L` is the set of
  subsets `Gal(L/E)` of `Gal(L/K)`, where `E/K` is finite

- `galBasis K L`. Given a field extension `L/K`, this is the filter basis on `L ‚âÉ‚Çê[K] L` whose
  sets are `Gal(L/E)` for intermediate fields `E` with `E/K` finite.

- `galGroupBasis K L`. This is the same as `galBasis K L`, but with the added structure
  that it is a group filter basis on `L ‚âÉ‚Çê[K] L`, rather than just a filter basis.

- `krullTopology K L`. Given a field extension `L/K`, this is the topology on `L ‚âÉ‚Çê[K] L`, induced
  by the group filter basis `galGroupBasis K L`.

## Main Results

- `krullTopology_t2 K L`. For an integral field extension `L/K`, the topology `krullTopology K L`
  is Hausdorff.

- `krullTopology_totallyDisconnected K L`. For an integral field extension `L/K`, the topology
  `krullTopology K L` is totally disconnected.

## Notations

- In docstrings, we will write `Gal(L/E)` to denote the fixing subgroup of an intermediate field
  `E`. That is, `Gal(L/E)` is the subgroup of `L ‚âÉ‚Çê[K] L` consisting of automorphisms that fix
  every element of `E`. In particular, we distinguish between `L ‚âÉ‚Çê[E] L` and `Gal(L/E)`, since the
  former is defined to be a subgroup of `L ‚âÉ‚Çê[K] L`, while the latter is a group in its own right.

## Implementation Notes

- `krullTopology K L` is defined as an instance for type class inference.
-/

open scoped Pointwise

/-- Mapping intermediate fields along the identity does not change them -/
theorem IntermediateField.map_id {K L : Type*} [Field K] [Field L] [Algebra K L]
    (E : IntermediateField K L) : E.map (AlgHom.id K L) = E :=
  SetLike.coe_injective <| Set.image_id _

/-- Mapping a finite dimensional intermediate field along an algebra equivalence gives
a finite-dimensional intermediate field. -/
instance im_finiteDimensional {K L : Type*} [Field K] [Field L] [Algebra K L]
    {E : IntermediateField K L} (œÉ : L ‚âÉ‚Çê[K] L) [FiniteDimensional K E] :
    FiniteDimensional K (E.map œÉ.toAlgHom) :=
  LinearEquiv.finiteDimensional (IntermediateField.intermediateFieldMap œÉ E).toLinearEquiv

/-- Given a field extension `L/K`, `finiteExts K L` is the set of
intermediate field extensions `L/E/K` such that `E/K` is finite -/
def finiteExts (K : Type*) [Field K] (L : Type*) [Field L] [Algebra K L] :
    Set (IntermediateField K L) :=
  {E | FiniteDimensional K E}

/-- Given a field extension `L/K`, `fixedByFinite K L` is the set of
subsets `Gal(L/E)` of `L ‚âÉ‚Çê[K] L`, where `E/K` is finite -/
def fixedByFinite (K L : Type*) [Field K] [Field L] [Algebra K L] : Set (Subgroup (L ‚âÉ‚Çê[K] L)) :=
  IntermediateField.fixingSubgroup '' finiteExts K L

/-- For a field extension `L/K`, the intermediate field `K` is finite-dimensional over `K` -/
theorem IntermediateField.finiteDimensional_bot (K L : Type*) [Field K] [Field L] [Algebra K L] :
    FiniteDimensional K (‚ä• : IntermediateField K L) :=
  .of_rank_eq_one IntermediateField.rank_bot

/-- This lemma says that `Gal(L/K) = L ‚âÉ‚Çê[K] L` -/
theorem IntermediateField.fixingSubgroup.bot {K L : Type*} [Field K] [Field L] [Algebra K L] :
    IntermediateField.fixingSubgroup (‚ä• : IntermediateField K L) = ‚ä§ := by
  ext f
  refine ‚ü®fun _ => Subgroup.mem_top _, fun _ => ?_‚ü©
  rintro ‚ü®x, hx : x ‚àà (‚ä• : IntermediateField K L)‚ü©
  rw [IntermediateField.mem_bot] at hx
  rcases hx with ‚ü®y, rfl‚ü©
  exact f.commutes y

/-- If `L/K` is a field extension, then we have `Gal(L/K) ‚àà fixedByFinite K L` -/
theorem top_fixedByFinite {K L : Type*} [Field K] [Field L] [Algebra K L] :
    ‚ä§ ‚àà fixedByFinite K L :=
  ‚ü®‚ä•, IntermediateField.finiteDimensional_bot K L, IntermediateField.fixingSubgroup.bot‚ü©

/-- If `E1` and `E2` are finite-dimensional intermediate fields, then so is their compositum.
This rephrases a result already in mathlib so that it is compatible with our type classes -/
theorem finiteDimensional_sup {K L : Type*} [Field K] [Field L] [Algebra K L]
    (E1 E2 : IntermediateField K L) (_ : FiniteDimensional K E1) (_ : FiniteDimensional K E2) :
    FiniteDimensional K (‚Ü•(E1 ‚äî E2)) :=
  IntermediateField.finiteDimensional_sup E1 E2

/-- An element of `L ‚âÉ‚Çê[K] L` is in `Gal(L/E)` if and only if it fixes every element of `E`-/
theorem IntermediateField.mem_fixingSubgroup_iff {K L : Type*} [Field K] [Field L] [Algebra K L]
    (E : IntermediateField K L) (œÉ : L ‚âÉ‚Çê[K] L) : œÉ ‚àà E.fixingSubgroup ‚Üî ‚àÄ x : L, x ‚àà E ‚Üí œÉ x = x :=
  ‚ü®fun hœÉ x hx => hœÉ ‚ü®x, hx‚ü©, fun h ‚ü®x, hx‚ü© => h x hx‚ü©

/-- The map `E ‚Ü¶ Gal(L/E)` is inclusion-reversing -/
theorem IntermediateField.fixingSubgroup.antimono {K L : Type*} [Field K] [Field L] [Algebra K L]
    {E1 E2 : IntermediateField K L} (h12 : E1 ‚â§ E2) : E2.fixingSubgroup ‚â§ E1.fixingSubgroup := by
  rintro œÉ hœÉ ‚ü®x, hx‚ü©
  exact hœÉ ‚ü®x, h12 hx‚ü©

/-- Given a field extension `L/K`, `galBasis K L` is the filter basis on `L ‚âÉ‚Çê[K] L` whose sets
are `Gal(L/E)` for intermediate fields `E` with `E/K` finite dimensional -/
def galBasis (K L : Type*) [Field K] [Field L] [Algebra K L] : FilterBasis (L ‚âÉ‚Çê[K] L) where
  sets := (fun g => g.carrier) '' fixedByFinite K L
  nonempty := ‚ü®‚ä§, ‚ä§, top_fixedByFinite, rfl‚ü©
  inter_sets := by
    rintro X Y ‚ü®H1, ‚ü®E1, h_E1, rfl‚ü©, rfl‚ü© ‚ü®H2, ‚ü®E2, h_E2, rfl‚ü©, rfl‚ü©
    use (IntermediateField.fixingSubgroup (E1 ‚äî E2)).carrier
    refine ‚ü®‚ü®_, ‚ü®_, finiteDimensional_sup E1 E2 h_E1 h_E2, rfl‚ü©, rfl‚ü©, ?_‚ü©
    rw [Set.subset_inter_iff]
    exact
      ‚ü®IntermediateField.fixingSubgroup.antimono le_sup_left,
        IntermediateField.fixingSubgroup.antimono le_sup_right‚ü©

/-- A subset of `L ‚âÉ‚Çê[K] L` is a member of `galBasis K L` if and only if it is the underlying set
of `Gal(L/E)` for some finite subextension `E/K`-/
theorem mem_galBasis_iff (K L : Type*) [Field K] [Field L] [Algebra K L] (U : Set (L ‚âÉ‚Çê[K] L)) :
    U ‚àà galBasis K L ‚Üî U ‚àà (fun g => g.carrier) '' fixedByFinite K L :=
  Iff.rfl

/-- For a field extension `L/K`, `galGroupBasis K L` is the group filter basis on `L ‚âÉ‚Çê[K] L`
whose sets are `Gal(L/E)` for finite subextensions `E/K` -/
def galGroupBasis (K L : Type*) [Field K] [Field L] [Algebra K L] :
    GroupFilterBasis (L ‚âÉ‚Çê[K] L) where
  toFilterBasis := galBasis K L
  one' := fun ‚ü®H, _, h2‚ü© => h2 ‚ñ∏ H.one_mem
  mul' {U} hU :=
    ‚ü®U, hU, by
      rcases hU with ‚ü®H, _, rfl‚ü©
      rintro x ‚ü®a, haH, b, hbH, rfl‚ü©
      exact H.mul_mem haH hbH‚ü©
  inv' {U} hU :=
    ‚ü®U, hU, by
      rcases hU with ‚ü®H, _, rfl‚ü©
      exact fun _ => H.inv_mem'‚ü©
  conj' := by
    rintro œÉ U ‚ü®H, ‚ü®E, hE, rfl‚ü©, rfl‚ü©
    let F : IntermediateField K L := E.map œÉ.symm.toAlgHom
    refine ‚ü®F.fixingSubgroup.carrier, ‚ü®‚ü®F.fixingSubgroup, ‚ü®F, ?_, rfl‚ü©, rfl‚ü©, fun g hg => ?_‚ü©‚ü©
    ¬∑ have : FiniteDimensional K E := hE
      apply im_finiteDimensional œÉ.symm
    change œÉ * g * œÉ‚Åª¬π ‚àà E.fixingSubgroup
    rw [IntermediateField.mem_fixingSubgroup_iff]
    intro x hx
    change œÉ (g (œÉ‚Åª¬π x)) = x
    have h_in_F : œÉ‚Åª¬π x ‚àà F := ‚ü®x, hx, by dsimp; rw [‚Üê AlgEquiv.invFun_eq_symm]; rfl‚ü©
    have h_g_fix : g (œÉ‚Åª¬π x) = œÉ‚Åª¬π x := by
      rw [Subgroup.mem_carrier, IntermediateField.mem_fixingSubgroup_iff F g] at hg
      exact hg (œÉ‚Åª¬π x) h_in_F
    rw [h_g_fix]
    change œÉ (œÉ‚Åª¬π x) = x
    exact AlgEquiv.apply_symm_apply œÉ x

/-- For a field extension `L/K`, `krullTopology K L` is the topological space structure on
`L ‚âÉ‚Çê[K] L` induced by the group filter basis `galGroupBasis K L` -/
instance krullTopology (K L : Type*) [Field K] [Field L] [Algebra K L] :
    TopologicalSpace (L ‚âÉ‚Çê[K] L) :=
  GroupFilterBasis.topology (galGroupBasis K L)

/-- For a field extension `L/K`, the Krull topology on `L ‚âÉ‚Çê[K] L` makes it a topological group. -/
instance (K L : Type*) [Field K] [Field L] [Algebra K L] : IsTopologicalGroup (L ‚âÉ‚Çê[K] L) :=
  GroupFilterBasis.isTopologicalGroup (galGroupBasis K L)

open scoped Topology in
lemma krullTopology_mem_nhds_one_iff (K L : Type*) [Field K] [Field L] [Algebra K L]
    (s : Set (L ‚âÉ‚Çê[K] L)) : s ‚àà ùìù 1 ‚Üî ‚àÉ E : IntermediateField K L,
    FiniteDimensional K E ‚àß (E.fixingSubgroup : Set (L ‚âÉ‚Çê[K] L)) ‚äÜ s := by
  rw [GroupFilterBasis.nhds_one_eq]
  constructor
  ¬∑ rintro ‚ü®-, ‚ü®-, ‚ü®E, fin, rfl‚ü©, rfl‚ü©, hE‚ü©
    exact ‚ü®E, fin, hE‚ü©
  ¬∑ rintro ‚ü®E, fin, hE‚ü©
    exact ‚ü®E.fixingSubgroup, ‚ü®E.fixingSubgroup, ‚ü®E, fin, rfl‚ü©, rfl‚ü©, hE‚ü©

open scoped Topology in
lemma krullTopology_mem_nhds_one_iff_of_normal (K L : Type*) [Field K] [Field L] [Algebra K L]
    [Normal K L] (s : Set (L ‚âÉ‚Çê[K] L)) : s ‚àà ùìù 1 ‚Üî ‚àÉ E : IntermediateField K L,
    FiniteDimensional K E ‚àß Normal K E ‚àß (E.fixingSubgroup : Set (L ‚âÉ‚Çê[K] L)) ‚äÜ s := by
  rw [krullTopology_mem_nhds_one_iff]
  refine ‚ü®fun ‚ü®E, _, hE‚ü© ‚Ü¶ ?_, fun ‚ü®E, hE‚ü© ‚Ü¶ ‚ü®E, hE.1, hE.2.2‚ü©‚ü©
  use (IntermediateField.normalClosure K E L)
  simp only [normalClosure.is_finiteDimensional K E L, normalClosure.normal K E L, true_and]
  exact le_trans (E.fixingSubgroup_anti E.le_normalClosure) hE

section KrullT2

open scoped Topology Filter

/-- Let `L/E/K` be a tower of fields with `E/K` finite. Then `Gal(L/E)` is an open subgroup of
  `L ‚âÉ‚Çê[K] L`. -/
theorem IntermediateField.fixingSubgroup_isOpen {K L : Type*} [Field K] [Field L] [Algebra K L]
    (E : IntermediateField K L) [FiniteDimensional K E] :
    IsOpen (E.fixingSubgroup : Set (L ‚âÉ‚Çê[K] L)) := by
  have h_basis : E.fixingSubgroup.carrier ‚àà galGroupBasis K L :=
    ‚ü®E.fixingSubgroup, ‚ü®E, ‚Äπ_‚Ä∫, rfl‚ü©, rfl‚ü©
  have h_nhd := GroupFilterBasis.mem_nhds_one (galGroupBasis K L) h_basis
  exact Subgroup.isOpen_of_mem_nhds _ h_nhd

/-- Given a tower of fields `L/E/K`, with `E/K` finite, the subgroup `Gal(L/E) ‚â§ L ‚âÉ‚Çê[K] L` is
  closed. -/
theorem IntermediateField.fixingSubgroup_isClosed {K L : Type*} [Field K] [Field L] [Algebra K L]
    (E : IntermediateField K L) [FiniteDimensional K E] :
    IsClosed (E.fixingSubgroup : Set (L ‚âÉ‚Çê[K] L)) :=
  OpenSubgroup.isClosed ‚ü®E.fixingSubgroup, E.fixingSubgroup_isOpen‚ü©

/-- If `L/K` is an algebraic extension, then the Krull topology on `L ‚âÉ‚Çê[K] L` is Hausdorff. -/
theorem krullTopology_t2 {K L : Type*} [Field K] [Field L] [Algebra K L]
    [Algebra.IsIntegral K L] : T2Space (L ‚âÉ‚Çê[K] L) :=
  { t2 := fun f g hfg => by
      let œÜ := f‚Åª¬π * g
      obtain ‚ü®x, hx‚ü© := DFunLike.exists_ne hfg
      have hœÜx : œÜ x ‚â† x := by
        apply ne_of_apply_ne f
        change f (f.symm (g x)) ‚â† f x
        rw [AlgEquiv.apply_symm_apply f (g x), ne_comm]
        exact hx
      let E : IntermediateField K L := IntermediateField.adjoin K {x}
      let h_findim : FiniteDimensional K E := IntermediateField.adjoin.finiteDimensional
        (Algebra.IsIntegral.isIntegral x)
      let H := E.fixingSubgroup
      have h_basis : (H : Set (L ‚âÉ‚Çê[K] L)) ‚àà galGroupBasis K L := ‚ü®H, ‚ü®E, ‚ü®h_findim, rfl‚ü©‚ü©, rfl‚ü©
      have h_nhd := GroupFilterBasis.mem_nhds_one (galGroupBasis K L) h_basis
      rw [mem_nhds_iff] at h_nhd
      rcases h_nhd with ‚ü®W, hWH, hW_open, hW_1‚ü©
      refine ‚ü®f ‚Ä¢ W, g ‚Ä¢ W,
        ‚ü®hW_open.leftCoset f, hW_open.leftCoset g, ‚ü®1, hW_1, mul_one _‚ü©, ‚ü®1, hW_1, mul_one _‚ü©, ?_‚ü©‚ü©
      rw [Set.disjoint_left]
      rintro œÉ ‚ü®w1, hw1, h‚ü© ‚ü®w2, hw2, rfl‚ü©
      dsimp at h
      rw [eq_inv_mul_iff_mul_eq.symm, ‚Üê mul_assoc, mul_inv_eq_iff_eq_mul.symm] at h
      have h_in_H : w1 * w2‚Åª¬π ‚àà H := H.mul_mem (hWH hw1) (H.inv_mem (hWH hw2))
      rw [h] at h_in_H
      change œÜ ‚àà E.fixingSubgroup at h_in_H
      rw [IntermediateField.mem_fixingSubgroup_iff] at h_in_H
      specialize h_in_H x
      have hxE : x ‚àà E := by
        apply IntermediateField.subset_adjoin
        apply Set.mem_singleton
      exact hœÜx (h_in_H hxE) }

end KrullT2

section TotallyDisconnected

/-- If `L/K` is an algebraic field extension, then the Krull topology on `L ‚âÉ‚Çê[K] L` is
  totally disconnected. -/
theorem krullTopology_totallyDisconnected {K L : Type*} [Field K] [Field L] [Algebra K L]
    [Algebra.IsIntegral K L] : IsTotallyDisconnected (Set.univ : Set (L ‚âÉ‚Çê[K] L)) := by
  apply isTotallyDisconnected_of_isClopen_set
  intro œÉ œÑ h_diff
  have hœÉœÑ : œÉ‚Åª¬π * œÑ ‚â† 1 := by rwa [Ne, inv_mul_eq_one]
  rcases DFunLike.exists_ne hœÉœÑ with ‚ü®x, hx : (œÉ‚Åª¬π * œÑ) x ‚â† x‚ü©
  let E := IntermediateField.adjoin K ({x} : Set L)
  haveI := IntermediateField.adjoin.finiteDimensional
    (Algebra.IsIntegral.isIntegral (R := K) x)
  refine ‚ü®œÉ ‚Ä¢ E.fixingSubgroup,
    ‚ü®E.fixingSubgroup_isClosed.leftCoset œÉ, E.fixingSubgroup_isOpen.leftCoset œÉ‚ü©,
    ‚ü®1, E.fixingSubgroup.one_mem', mul_one œÉ‚ü©, ?_‚ü©
  simp only [mem_leftCoset_iff, SetLike.mem_coe, IntermediateField.mem_fixingSubgroup_iff,
    not_forall]
  exact ‚ü®x, IntermediateField.mem_adjoin_simple_self K x, hx‚ü©

instance {K L : Type*} [Field K] [Field L] [Algebra K L] [Algebra.IsIntegral K L] :
    TotallyDisconnectedSpace (L ‚âÉ‚Çê[K] L) where
  isTotallyDisconnected_univ := krullTopology_totallyDisconnected

end TotallyDisconnected

@[simp] lemma IntermediateField.fixingSubgroup_top (K L : Type*) [Field K] [Field L] [Algebra K L] :
    IntermediateField.fixingSubgroup (‚ä§ : IntermediateField K L) = ‚ä• := by
  ext
  simp [mem_fixingSubgroup_iff, DFunLike.ext_iff]

@[simp] lemma IntermediateField.fixingSubgroup_bot (K L : Type*) [Field K] [Field L] [Algebra K L] :
    IntermediateField.fixingSubgroup (‚ä• : IntermediateField K L) = ‚ä§ := by
  ext
  simp [mem_fixingSubgroup_iff, mem_bot]

instance krullTopology_discreteTopology_of_finiteDimensional (K L : Type*) [Field K] [Field L]
    [Algebra K L] [FiniteDimensional K L] : DiscreteTopology (L ‚âÉ‚Çê[K] L) := by
  rw [discreteTopology_iff_isOpen_singleton_one]
  change IsOpen ((‚ä• : Subgroup (L ‚âÉ‚Çê[K] L)) : Set (L ‚âÉ‚Çê[K] L))
  rw [‚Üê IntermediateField.fixingSubgroup_top]
  exact IntermediateField.fixingSubgroup_isOpen ‚ä§
