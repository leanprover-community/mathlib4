/-
Copyright (c) 2022 Sebastian Monnet. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sebastian Monnet
-/
import Mathlib.FieldTheory.Galois
import Mathlib.Topology.Algebra.FilterBasis
import Mathlib.Topology.Algebra.OpenSubgroup
import Mathlib.Tactic.ByContra

#align_import field_theory.krull_topology from "leanprover-community/mathlib"@"039a089d2a4b93c761b234f3e5f5aeb752bac60f"

/-!
# Krull topology

We define the Krull topology on `L â‰ƒâ‚[K] L` for an arbitrary field extension `L/K`. In order to do
this, we first define a `GroupFilterBasis` on `L â‰ƒâ‚[K] L`, whose sets are `E.fixingSubgroup` for
all intermediate fields `E` with `E/K` finite dimensional.

## Main Definitions

- `finiteExts K L`. Given a field extension `L/K`, this is the set of intermediate fields that are
  finite-dimensional over `K`.

- `fixedByFinite K L`. Given a field extension `L/K`, `fixedByFinite K L` is the set of
  subsets `Gal(L/E)` of `Gal(L/K)`, where `E/K` is finite

- `galBasis K L`. Given a field extension `L/K`, this is the filter basis on `L â‰ƒâ‚[K] L` whose
  sets are `Gal(L/E)` for intermediate fields `E` with `E/K` finite.

- `galGroupBasis K L`. This is the same as `galBasis K L`, but with the added structure
  that it is a group filter basis on `L â‰ƒâ‚[K] L`, rather than just a filter basis.

- `krullTopology K L`. Given a field extension `L/K`, this is the topology on `L â‰ƒâ‚[K] L`, induced
  by the group filter basis `galGroupBasis K L`.

## Main Results

- `krullTopology_t2 K L`. For an integral field extension `L/K`, the topology `krullTopology K L`
  is Hausdorff.

- `krullTopology_totallyDisconnected K L`. For an integral field extension `L/K`, the topology
  `krullTopology K L` is totally disconnected.

## Notations

- In docstrings, we will write `Gal(L/E)` to denote the fixing subgroup of an intermediate field
  `E`. That is, `Gal(L/E)` is the subgroup of `L â‰ƒâ‚[K] L` consisting of automorphisms that fix
  every element of `E`. In particular, we distinguish between `L â‰ƒâ‚[E] L` and `Gal(L/E)`, since the
  former is defined to be a subgroup of `L â‰ƒâ‚[K] L`, while the latter is a group in its own right.

## Implementation Notes

- `krullTopology K L` is defined as an instance for type class inference.
-/


open scoped Classical

/-- Mapping intermediate fields along algebra equivalences preserves the partial order -/
theorem IntermediateField.map_mono {K L M : Type*} [Field K] [Field L] [Field M] [Algebra K L]
    [Algebra K M] {E1 E2 : IntermediateField K L} (e : L â‰ƒâ‚[K] M) (h12 : E1 â‰¤ E2) :
    E1.map e.toAlgHom â‰¤ E2.map e.toAlgHom :=
  Set.image_subset e h12
#align intermediate_field.map_mono IntermediateField.map_mono

/-- Mapping intermediate fields along the identity does not change them -/
theorem IntermediateField.map_id {K L : Type*} [Field K] [Field L] [Algebra K L]
    (E : IntermediateField K L) : E.map (AlgHom.id K L) = E :=
  SetLike.coe_injective <| Set.image_id _
#align intermediate_field.map_id IntermediateField.map_id

/-- Mapping a finite dimensional intermediate field along an algebra equivalence gives
a finite-dimensional intermediate field. -/
instance im_finiteDimensional {K L : Type*} [Field K] [Field L] [Algebra K L]
    {E : IntermediateField K L} (Ïƒ : L â‰ƒâ‚[K] L) [FiniteDimensional K E] :
    FiniteDimensional K (E.map Ïƒ.toAlgHom) :=
  LinearEquiv.finiteDimensional (IntermediateField.intermediateFieldMap Ïƒ E).toLinearEquiv
#align im_finite_dimensional im_finiteDimensional

/-- Given a field extension `L/K`, `finiteExts K L` is the set of
intermediate field extensions `L/E/K` such that `E/K` is finite -/
def finiteExts (K : Type*) [Field K] (L : Type*) [Field L] [Algebra K L] :
    Set (IntermediateField K L) :=
  {E | FiniteDimensional K E}
#align finite_exts finiteExts

/-- Given a field extension `L/K`, `fixedByFinite K L` is the set of
subsets `Gal(L/E)` of `L â‰ƒâ‚[K] L`, where `E/K` is finite -/
def fixedByFinite (K L : Type*) [Field K] [Field L] [Algebra K L] : Set (Subgroup (L â‰ƒâ‚[K] L)) :=
  IntermediateField.fixingSubgroup '' finiteExts K L
#align fixed_by_finite fixedByFinite

/-- For a field extension `L/K`, the intermediate field `K` is finite-dimensional over `K` -/
theorem IntermediateField.finiteDimensional_bot (K L : Type*) [Field K] [Field L] [Algebra K L] :
    FiniteDimensional K (âŠ¥ : IntermediateField K L) :=
  finiteDimensional_of_rank_eq_one IntermediateField.rank_bot
#align intermediate_field.finite_dimensional_bot IntermediateField.finiteDimensional_bot

/-- This lemma says that `Gal(L/K) = L â‰ƒâ‚[K] L` -/
theorem IntermediateField.fixingSubgroup.bot {K L : Type*} [Field K] [Field L] [Algebra K L] :
    IntermediateField.fixingSubgroup (âŠ¥ : IntermediateField K L) = âŠ¤ := by
  ext f
  -- âŠ¢ f âˆˆ fixingSubgroup âŠ¥ â†” f âˆˆ âŠ¤
  refine' âŸ¨fun _ => Subgroup.mem_top _, fun _ => _âŸ©
  -- âŠ¢ f âˆˆ fixingSubgroup âŠ¥
  rintro âŸ¨x, hx : x âˆˆ (âŠ¥ : IntermediateField K L)âŸ©
  -- âŠ¢ f â€¢ â†‘{ val := x, property := hx } = â†‘{ val := x, property := hx }
  rw [IntermediateField.mem_bot] at hx
  -- âŠ¢ f â€¢ â†‘{ val := x, property := hxâœ } = â†‘{ val := x, property := hxâœ }
  rcases hx with âŸ¨y, rflâŸ©
  -- âŠ¢ f â€¢ â†‘{ val := â†‘(algebraMap K L) y, property := hx } = â†‘{ val := â†‘(algebraMap â€¦
  exact f.commutes y
  -- ğŸ‰ no goals
#align intermediate_field.fixing_subgroup.bot IntermediateField.fixingSubgroup.bot

/-- If `L/K` is a field extension, then we have `Gal(L/K) âˆˆ fixedByFinite K L` -/
theorem top_fixedByFinite {K L : Type*} [Field K] [Field L] [Algebra K L] :
    âŠ¤ âˆˆ fixedByFinite K L :=
  âŸ¨âŠ¥, IntermediateField.finiteDimensional_bot K L, IntermediateField.fixingSubgroup.botâŸ©
#align top_fixed_by_finite top_fixedByFinite

/-- If `E1` and `E2` are finite-dimensional intermediate fields, then so is their compositum.
This rephrases a result already in mathlib so that it is compatible with our type classes -/
theorem finiteDimensional_sup {K L : Type*} [Field K] [Field L] [Algebra K L]
    (E1 E2 : IntermediateField K L) (_ : FiniteDimensional K E1) (_ : FiniteDimensional K E2) :
    FiniteDimensional K (â†¥(E1 âŠ” E2)) :=
  IntermediateField.finiteDimensional_sup E1 E2
#align finite_dimensional_sup finiteDimensional_sup

/-- An element of `L â‰ƒâ‚[K] L` is in `Gal(L/E)` if and only if it fixes every element of `E`-/
theorem IntermediateField.mem_fixingSubgroup_iff {K L : Type*} [Field K] [Field L] [Algebra K L]
    (E : IntermediateField K L) (Ïƒ : L â‰ƒâ‚[K] L) : Ïƒ âˆˆ E.fixingSubgroup â†” âˆ€ x : L, x âˆˆ E â†’ Ïƒ x = x :=
  âŸ¨fun hÏƒ x hx => hÏƒ âŸ¨x, hxâŸ©, fun h âŸ¨x, hxâŸ© => h x hxâŸ©
#align intermediate_field.mem_fixing_subgroup_iff IntermediateField.mem_fixingSubgroup_iff

/-- The map `E â†¦ Gal(L/E)` is inclusion-reversing -/
theorem IntermediateField.fixingSubgroup.antimono {K L : Type*} [Field K] [Field L] [Algebra K L]
    {E1 E2 : IntermediateField K L} (h12 : E1 â‰¤ E2) : E2.fixingSubgroup â‰¤ E1.fixingSubgroup := by
  rintro Ïƒ hÏƒ âŸ¨x, hxâŸ©
  -- âŠ¢ Ïƒ â€¢ â†‘{ val := x, property := hx } = â†‘{ val := x, property := hx }
  exact hÏƒ âŸ¨x, h12 hxâŸ©
  -- ğŸ‰ no goals
#align intermediate_field.fixing_subgroup.antimono IntermediateField.fixingSubgroup.antimono

/-- Given a field extension `L/K`, `galBasis K L` is the filter basis on `L â‰ƒâ‚[K] L` whose sets
are `Gal(L/E)` for intermediate fields `E` with `E/K` finite dimensional -/
def galBasis (K L : Type*) [Field K] [Field L] [Algebra K L] : FilterBasis (L â‰ƒâ‚[K] L) where
  sets := (fun g => g.carrier) '' fixedByFinite K L
  nonempty := âŸ¨âŠ¤, âŠ¤, top_fixedByFinite, rflâŸ©
  inter_sets := by
    rintro X Y âŸ¨H1, âŸ¨E1, h_E1, rflâŸ©, rflâŸ© âŸ¨H2, âŸ¨E2, h_E2, rflâŸ©, rflâŸ©
    -- âŠ¢ âˆƒ z, z âˆˆ (fun g => g.carrier) '' fixedByFinite K L âˆ§ z âŠ† (fun g => g.carrier â€¦
    use (IntermediateField.fixingSubgroup (E1 âŠ” E2)).carrier
    -- âŠ¢ (IntermediateField.fixingSubgroup (E1 âŠ” E2)).toSubmonoid.toSubsemigroup.carr â€¦
    refine' âŸ¨âŸ¨_, âŸ¨_, finiteDimensional_sup E1 E2 h_E1 h_E2, rflâŸ©, rflâŸ©, _âŸ©
    -- âŠ¢ (IntermediateField.fixingSubgroup (E1 âŠ” E2)).toSubmonoid.toSubsemigroup.carr â€¦
    rw [Set.subset_inter_iff]
    -- âŠ¢ (IntermediateField.fixingSubgroup (E1 âŠ” E2)).toSubmonoid.toSubsemigroup.carr â€¦
    exact
      âŸ¨IntermediateField.fixingSubgroup.antimono le_sup_left,
        IntermediateField.fixingSubgroup.antimono le_sup_rightâŸ©
#align gal_basis galBasis

/-- A subset of `L â‰ƒâ‚[K] L` is a member of `galBasis K L` if and only if it is the underlying set
of `Gal(L/E)` for some finite subextension `E/K`-/
theorem mem_galBasis_iff (K L : Type*) [Field K] [Field L] [Algebra K L] (U : Set (L â‰ƒâ‚[K] L)) :
    U âˆˆ galBasis K L â†” U âˆˆ (fun g => g.carrier) '' fixedByFinite K L :=
  Iff.rfl
#align mem_gal_basis_iff mem_galBasis_iff

/-- For a field extension `L/K`, `galGroupBasis K L` is the group filter basis on `L â‰ƒâ‚[K] L`
whose sets are `Gal(L/E)` for finite subextensions `E/K` -/
def galGroupBasis (K L : Type*) [Field K] [Field L] [Algebra K L] :
    GroupFilterBasis (L â‰ƒâ‚[K] L) where
  toFilterBasis := galBasis K L
  one' := fun âŸ¨H, _, h2âŸ© => h2 â–¸ H.one_mem
  mul' {U} hU :=
    âŸ¨U, hU, by
      rcases hU with âŸ¨H, _, rflâŸ©
      -- âŠ¢ (fun g => g.carrier) H * (fun g => g.carrier) H âŠ† (fun g => g.carrier) H
      rintro x âŸ¨a, b, haH, hbH, rflâŸ©
      -- âŠ¢ (fun x x_1 => x * x_1) a b âˆˆ (fun g => g.carrier) H
      exact H.mul_mem haH hbHâŸ©
      -- ğŸ‰ no goals
  inv' {U} hU :=
    âŸ¨U, hU, by
      rcases hU with âŸ¨H, _, rflâŸ©
      -- âŠ¢ (fun g => g.carrier) H âŠ† (fun x => xâ»Â¹) â»Â¹' (fun g => g.carrier) H
      exact fun _ => H.inv_mem'âŸ©
      -- ğŸ‰ no goals
  conj' := by
    rintro Ïƒ U âŸ¨H, âŸ¨E, hE, rflâŸ©, rflâŸ©
    -- âŠ¢ âˆƒ V, V âˆˆ (galBasis K L).sets âˆ§ V âŠ† (fun x => Ïƒ * x * Ïƒâ»Â¹) â»Â¹' (fun g => g.ca â€¦
    let F : IntermediateField K L := E.map Ïƒ.symm.toAlgHom
    -- âŠ¢ âˆƒ V, V âˆˆ (galBasis K L).sets âˆ§ V âŠ† (fun x => Ïƒ * x * Ïƒâ»Â¹) â»Â¹' (fun g => g.ca â€¦
    refine' âŸ¨F.fixingSubgroup.carrier, âŸ¨âŸ¨F.fixingSubgroup, âŸ¨F, _, rflâŸ©, rflâŸ©, fun g hg => _âŸ©âŸ©
    -- âŠ¢ F âˆˆ finiteExts K L
    Â· have : FiniteDimensional K E := hE
      -- âŠ¢ F âˆˆ finiteExts K L
      apply im_finiteDimensional Ïƒ.symm
      -- ğŸ‰ no goals
    change Ïƒ * g * Ïƒâ»Â¹ âˆˆ E.fixingSubgroup
    -- âŠ¢ Ïƒ * g * Ïƒâ»Â¹ âˆˆ IntermediateField.fixingSubgroup E
    rw [IntermediateField.mem_fixingSubgroup_iff]
    -- âŠ¢ âˆ€ (x : L), x âˆˆ E â†’ â†‘(Ïƒ * g * Ïƒâ»Â¹) x = x
    intro x hx
    -- âŠ¢ â†‘(Ïƒ * g * Ïƒâ»Â¹) x = x
    change Ïƒ (g (Ïƒâ»Â¹ x)) = x
    -- âŠ¢ â†‘Ïƒ (â†‘g (â†‘Ïƒâ»Â¹ x)) = x
    have h_in_F : Ïƒâ»Â¹ x âˆˆ F := âŸ¨x, hx, by dsimp; rw [â† AlgEquiv.invFun_eq_symm]; rflâŸ©
    -- âŠ¢ â†‘Ïƒ (â†‘g (â†‘Ïƒâ»Â¹ x)) = x
    have h_g_fix : g (Ïƒâ»Â¹ x) = Ïƒâ»Â¹ x := by
      rw [Subgroup.mem_carrier, IntermediateField.mem_fixingSubgroup_iff F g] at hg
      exact hg (Ïƒâ»Â¹ x) h_in_F
    rw [h_g_fix]
    -- âŠ¢ â†‘Ïƒ (â†‘Ïƒâ»Â¹ x) = x
    change Ïƒ (Ïƒâ»Â¹ x) = x
    -- âŠ¢ â†‘Ïƒ (â†‘Ïƒâ»Â¹ x) = x
    exact AlgEquiv.apply_symm_apply Ïƒ x
    -- ğŸ‰ no goals
#align gal_group_basis galGroupBasis

/-- For a field extension `L/K`, `krullTopology K L` is the topological space structure on
`L â‰ƒâ‚[K] L` induced by the group filter basis `galGroupBasis K L` -/
instance krullTopology (K L : Type*) [Field K] [Field L] [Algebra K L] :
    TopologicalSpace (L â‰ƒâ‚[K] L) :=
  GroupFilterBasis.topology (galGroupBasis K L)
#align krull_topology krullTopology

/-- For a field extension `L/K`, the Krull topology on `L â‰ƒâ‚[K] L` makes it a topological group. -/
instance (K L : Type*) [Field K] [Field L] [Algebra K L] : TopologicalGroup (L â‰ƒâ‚[K] L) :=
  GroupFilterBasis.isTopologicalGroup (galGroupBasis K L)

section KrullT2

open scoped Topology Filter

/-- Let `L/E/K` be a tower of fields with `E/K` finite. Then `Gal(L/E)` is an open subgroup of
  `L â‰ƒâ‚[K] L`. -/
theorem IntermediateField.fixingSubgroup_isOpen {K L : Type*} [Field K] [Field L] [Algebra K L]
    (E : IntermediateField K L) [FiniteDimensional K E] :
    IsOpen (E.fixingSubgroup : Set (L â‰ƒâ‚[K] L)) := by
  have h_basis : E.fixingSubgroup.carrier âˆˆ galGroupBasis K L :=
    âŸ¨E.fixingSubgroup, âŸ¨E, â€¹_â€º, rflâŸ©, rflâŸ©
  have h_nhd := GroupFilterBasis.mem_nhds_one (galGroupBasis K L) h_basis
  -- âŠ¢ IsOpen â†‘(fixingSubgroup E)
  exact Subgroup.isOpen_of_mem_nhds _ h_nhd
  -- ğŸ‰ no goals
#align intermediate_field.fixing_subgroup_is_open IntermediateField.fixingSubgroup_isOpen

/-- Given a tower of fields `L/E/K`, with `E/K` finite, the subgroup `Gal(L/E) â‰¤ L â‰ƒâ‚[K] L` is
  closed. -/
theorem IntermediateField.fixingSubgroup_isClosed {K L : Type*} [Field K] [Field L] [Algebra K L]
    (E : IntermediateField K L) [FiniteDimensional K E] :
    IsClosed (E.fixingSubgroup : Set (L â‰ƒâ‚[K] L)) :=
  OpenSubgroup.isClosed âŸ¨E.fixingSubgroup, E.fixingSubgroup_isOpenâŸ©
#align intermediate_field.fixing_subgroup_is_closed IntermediateField.fixingSubgroup_isClosed

/-- If `L/K` is an algebraic extension, then the Krull topology on `L â‰ƒâ‚[K] L` is Hausdorff. -/
theorem krullTopology_t2 {K L : Type*} [Field K] [Field L] [Algebra K L]
    (h_int : Algebra.IsIntegral K L) : T2Space (L â‰ƒâ‚[K] L) :=
  { t2 := fun f g hfg => by
      let Ï† := fâ»Â¹ * g
      -- âŠ¢ âˆƒ u v, IsOpen u âˆ§ IsOpen v âˆ§ f âˆˆ u âˆ§ g âˆˆ v âˆ§ Disjoint u v
      cases' FunLike.exists_ne hfg with x hx
      -- âŠ¢ âˆƒ u v, IsOpen u âˆ§ IsOpen v âˆ§ f âˆˆ u âˆ§ g âˆˆ v âˆ§ Disjoint u v
      have hÏ†x : Ï† x â‰  x := by
        apply ne_of_apply_ne f
        change f (f.symm (g x)) â‰  f x
        rw [AlgEquiv.apply_symm_apply f (g x), ne_comm]
        exact hx
      let E : IntermediateField K L := IntermediateField.adjoin K {x}
      -- âŠ¢ âˆƒ u v, IsOpen u âˆ§ IsOpen v âˆ§ f âˆˆ u âˆ§ g âˆˆ v âˆ§ Disjoint u v
      let h_findim : FiniteDimensional K E := IntermediateField.adjoin.finiteDimensional (h_int x)
      -- âŠ¢ âˆƒ u v, IsOpen u âˆ§ IsOpen v âˆ§ f âˆˆ u âˆ§ g âˆˆ v âˆ§ Disjoint u v
      let H := E.fixingSubgroup
      -- âŠ¢ âˆƒ u v, IsOpen u âˆ§ IsOpen v âˆ§ f âˆˆ u âˆ§ g âˆˆ v âˆ§ Disjoint u v
      have h_basis : (H : Set (L â‰ƒâ‚[K] L)) âˆˆ galGroupBasis K L := âŸ¨H, âŸ¨E, âŸ¨h_findim, rflâŸ©âŸ©, rflâŸ©
      -- âŠ¢ âˆƒ u v, IsOpen u âˆ§ IsOpen v âˆ§ f âˆˆ u âˆ§ g âˆˆ v âˆ§ Disjoint u v
      have h_nhd := GroupFilterBasis.mem_nhds_one (galGroupBasis K L) h_basis
      -- âŠ¢ âˆƒ u v, IsOpen u âˆ§ IsOpen v âˆ§ f âˆˆ u âˆ§ g âˆˆ v âˆ§ Disjoint u v
      rw [mem_nhds_iff] at h_nhd
      -- âŠ¢ âˆƒ u v, IsOpen u âˆ§ IsOpen v âˆ§ f âˆˆ u âˆ§ g âˆˆ v âˆ§ Disjoint u v
      rcases h_nhd with âŸ¨W, hWH, hW_open, hW_1âŸ©
      -- âŠ¢ âˆƒ u v, IsOpen u âˆ§ IsOpen v âˆ§ f âˆˆ u âˆ§ g âˆˆ v âˆ§ Disjoint u v
      refine' âŸ¨leftCoset f W, leftCoset g W,
        âŸ¨hW_open.leftCoset f, hW_open.leftCoset g, âŸ¨1, hW_1, mul_one _âŸ©, âŸ¨1, hW_1, mul_one _âŸ©, _âŸ©âŸ©
      rw [Set.disjoint_left]
      -- âŠ¢ âˆ€ â¦ƒa : L â‰ƒâ‚[K] Lâ¦„, a âˆˆ leftCoset f W â†’ Â¬a âˆˆ leftCoset g W
      rintro Ïƒ âŸ¨w1, hw1, hâŸ© âŸ¨w2, hw2, rflâŸ©
      -- âŠ¢ False
      rw [eq_inv_mul_iff_mul_eq.symm, â† mul_assoc, mul_inv_eq_iff_eq_mul.symm] at h
      -- âŠ¢ False
      have h_in_H : w1 * w2â»Â¹ âˆˆ H := H.mul_mem (hWH hw1) (H.inv_mem (hWH hw2))
      -- âŠ¢ False
      rw [h] at h_in_H
      -- âŠ¢ False
      change Ï† âˆˆ E.fixingSubgroup at h_in_H
      -- âŠ¢ False
      rw [IntermediateField.mem_fixingSubgroup_iff] at h_in_H
      -- âŠ¢ False
      specialize h_in_H x
      -- âŠ¢ False
      have hxE : x âˆˆ E := by
        apply IntermediateField.subset_adjoin
        apply Set.mem_singleton
      exact hÏ†x (h_in_H hxE) }
      -- ğŸ‰ no goals
#align krull_topology_t2 krullTopology_t2

end KrullT2

section TotallyDisconnected

/-- If `L/K` is an algebraic field extension, then the Krull topology on `L â‰ƒâ‚[K] L` is
  totally disconnected. -/
theorem krullTopology_totallyDisconnected {K L : Type*} [Field K] [Field L] [Algebra K L]
    (h_int : Algebra.IsIntegral K L) : IsTotallyDisconnected (Set.univ : Set (L â‰ƒâ‚[K] L)) := by
  apply isTotallyDisconnected_of_clopen_set
  -- âŠ¢ Pairwise fun x y => âˆƒ U, IsClopen U âˆ§ x âˆˆ U âˆ§ Â¬y âˆˆ U
  intro Ïƒ Ï„ h_diff
  -- âŠ¢ âˆƒ U, IsClopen U âˆ§ Ïƒ âˆˆ U âˆ§ Â¬Ï„ âˆˆ U
  have hÏƒÏ„ : Ïƒâ»Â¹ * Ï„ â‰  1 := by rwa [Ne.def, inv_mul_eq_one]
  -- âŠ¢ âˆƒ U, IsClopen U âˆ§ Ïƒ âˆˆ U âˆ§ Â¬Ï„ âˆˆ U
  rcases FunLike.exists_ne hÏƒÏ„ with âŸ¨x, hx : (Ïƒâ»Â¹ * Ï„) x â‰  xâŸ©
  -- âŠ¢ âˆƒ U, IsClopen U âˆ§ Ïƒ âˆˆ U âˆ§ Â¬Ï„ âˆˆ U
  let E := IntermediateField.adjoin K ({x} : Set L)
  -- âŠ¢ âˆƒ U, IsClopen U âˆ§ Ïƒ âˆˆ U âˆ§ Â¬Ï„ âˆˆ U
  haveI := IntermediateField.adjoin.finiteDimensional (h_int x)
  -- âŠ¢ âˆƒ U, IsClopen U âˆ§ Ïƒ âˆˆ U âˆ§ Â¬Ï„ âˆˆ U
  refine' âŸ¨leftCoset Ïƒ E.fixingSubgroup,
    âŸ¨E.fixingSubgroup_isOpen.leftCoset Ïƒ, E.fixingSubgroup_isClosed.leftCoset ÏƒâŸ©,
    âŸ¨1, E.fixingSubgroup.one_mem', mul_one ÏƒâŸ©, _âŸ©
  simp only [mem_leftCoset_iff, SetLike.mem_coe, IntermediateField.mem_fixingSubgroup_iff,
    not_forall]
  exact âŸ¨x, IntermediateField.mem_adjoin_simple_self K x, hxâŸ©
  -- ğŸ‰ no goals
#align krull_topology_totally_disconnected krullTopology_totallyDisconnected

end TotallyDisconnected
