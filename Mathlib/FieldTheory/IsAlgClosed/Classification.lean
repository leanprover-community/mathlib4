/-
Copyright (c) 2022 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes
-/
import Mathlib.RingTheory.AlgebraicIndependent
import Mathlib.FieldTheory.IsAlgClosed.Basic
import Mathlib.Data.Polynomial.Cardinal
import Mathlib.Data.MvPolynomial.Cardinal
import Mathlib.Data.ZMod.Algebra

#align_import field_theory.is_alg_closed.classification from "leanprover-community/mathlib"@"0723536a0522d24fc2f159a096fb3304bef77472"

/-!
# Classification of Algebraically closed fields

This file contains results related to classifying algebraically closed fields.

## Main statements

* `IsAlgClosed.equivOfTranscendenceBasis` Two algebraically closed fields with the same
  characteristic and the same cardinality of transcendence basis are isomorphic.
* `IsAlgClosed.ringEquivOfCardinalEqOfCharEq` Two uncountable algebraically closed fields
  are isomorphic if they have the same characteristic and the same cardinality.
-/


universe u

open scoped Cardinal Polynomial

open Cardinal

section AlgebraicClosure

namespace Algebra.IsAlgebraic

variable (R L : Type u) [CommRing R] [CommRing L] [IsDomain L] [Algebra R L]

variable [NoZeroSMulDivisors R L] (halg : Algebra.IsAlgebraic R L)

theorem cardinal_mk_le_sigma_polynomial :
    #L â‰¤ #(Î£ p : R[X], { x : L // x âˆˆ (p.map (algebraMap R L)).roots }) :=
  @mk_le_of_injective L (Î£ p : R[X], {x : L | x âˆˆ (p.map (algebraMap R L)).roots})
    (fun x : L =>
      let p := Classical.indefiniteDescription _ (halg x)
      âŸ¨p.1, x, by
        dsimp
        -- âŠ¢ x âˆˆ Polynomial.roots (Polynomial.map (algebraMap R L) â†‘(Classical.indefinite â€¦
        have h : p.1.map (algebraMap R L) â‰  0 := by
          rw [Ne.def, â† Polynomial.degree_eq_bot,
            Polynomial.degree_map_eq_of_injective (NoZeroSMulDivisors.algebraMap_injective R L),
            Polynomial.degree_eq_bot]
          exact p.2.1
        erw [Polynomial.mem_roots h, Polynomial.IsRoot, Polynomial.eval_map, â† Polynomial.aeval_def,
          p.2.2]âŸ©)
    fun x y => by
      intro h
      -- âŠ¢ x = y
      simp at h
      -- âŠ¢ x = y
      refine' (Subtype.heq_iff_coe_eq _).1 h.2
      -- âŠ¢ âˆ€ (x_1 : L), x_1 âˆˆ {x_2 | x_2 âˆˆ Polynomial.roots (Polynomial.map (algebraMap â€¦
      simp only [h.1, iff_self_iff, forall_true_iff]
      -- ğŸ‰ no goals
#align algebra.is_algebraic.cardinal_mk_le_sigma_polynomial Algebra.IsAlgebraic.cardinal_mk_le_sigma_polynomial

/-- The cardinality of an algebraic extension is at most the maximum of the cardinality
of the base ring or `â„µâ‚€` -/
theorem cardinal_mk_le_max : #L â‰¤ max #R â„µâ‚€ :=
  calc
    #L â‰¤ #(Î£ p : R[X], { x : L // x âˆˆ (p.map (algebraMap R L)).roots }) :=
      cardinal_mk_le_sigma_polynomial R L halg
    _ = Cardinal.sum fun p : R[X] => #{x : L | x âˆˆ (p.map (algebraMap R L)).roots} := by
      rw [â† mk_sigma]; rfl
      -- âŠ¢ #((p : R[X]) Ã— { x // x âˆˆ Polynomial.roots (Polynomial.map (algebraMap R L)  â€¦
                       -- ğŸ‰ no goals
    _ â‰¤ Cardinal.sum.{u, u} fun _ : R[X] => â„µâ‚€ :=
      (sum_le_sum _ _ fun p => (Multiset.finite_toSet _).lt_aleph0.le)
    _ = #(R[X]) * â„µâ‚€ := (sum_const' _ _)
    _ â‰¤ max (max #(R[X]) â„µâ‚€) â„µâ‚€ := (mul_le_max _ _)
    _ â‰¤ max (max (max #R â„µâ‚€) â„µâ‚€) â„µâ‚€ :=
      (max_le_max (max_le_max Polynomial.cardinal_mk_le_max le_rfl) le_rfl)
    _ = max #R â„µâ‚€ := by simp only [max_assoc, max_comm â„µâ‚€, max_left_comm â„µâ‚€, max_self]
                        -- ğŸ‰ no goals
#align algebra.is_algebraic.cardinal_mk_le_max Algebra.IsAlgebraic.cardinal_mk_le_max

end Algebra.IsAlgebraic

end AlgebraicClosure

namespace IsAlgClosed

section Classification

noncomputable section

variable {R L K : Type*} [CommRing R]

variable [Field K] [Algebra R K]

variable [Field L] [Algebra R L]

variable {Î¹ : Type*} (v : Î¹ â†’ K)

variable {Îº : Type*} (w : Îº â†’ L)

variable (hv : AlgebraicIndependent R v)

theorem isAlgClosure_of_transcendence_basis [IsAlgClosed K] (hv : IsTranscendenceBasis R v) :
    IsAlgClosure (Algebra.adjoin R (Set.range v)) K :=
  letI := RingHom.domain_nontrivial (algebraMap R K)
  { alg_closed := by infer_instance
                     -- ğŸ‰ no goals
    algebraic := hv.isAlgebraic }
#align is_alg_closed.is_alg_closure_of_transcendence_basis IsAlgClosed.isAlgClosure_of_transcendence_basis

variable (hw : AlgebraicIndependent R w)

/-- setting `R` to be `ZMod (ringChar R)` this result shows that if two algebraically
closed fields have equipotent transcendence bases and the same characteristic then they are
isomorphic. -/
def equivOfTranscendenceBasis [IsAlgClosed K] [IsAlgClosed L] (e : Î¹ â‰ƒ Îº)
    (hv : IsTranscendenceBasis R v) (hw : IsTranscendenceBasis R w) : K â‰ƒ+* L := by
  letI := isAlgClosure_of_transcendence_basis v hv
  -- âŠ¢ K â‰ƒ+* L
  letI := isAlgClosure_of_transcendence_basis w hw
  -- âŠ¢ K â‰ƒ+* L
  have e : Algebra.adjoin R (Set.range v) â‰ƒ+* Algebra.adjoin R (Set.range w)
  -- âŠ¢ { x // x âˆˆ Algebra.adjoin R (Set.range v) } â‰ƒ+* { x // x âˆˆ Algebra.adjoin R  â€¦
  Â· refine' hv.1.aevalEquiv.symm.toRingEquiv.trans _
    -- âŠ¢ MvPolynomial Î¹ R â‰ƒ+* { x // x âˆˆ Algebra.adjoin R (Set.range w) }
    refine' (AlgEquiv.ofAlgHom (MvPolynomial.rename e)
      (MvPolynomial.rename e.symm) _ _).toRingEquiv.trans _
    Â· ext; simp
      -- âŠ¢ MvPolynomial.coeff mâœ (â†‘(AlgHom.comp (MvPolynomial.rename â†‘e) (MvPolynomial. â€¦
           -- ğŸ‰ no goals
    Â· ext; simp
      -- âŠ¢ MvPolynomial.coeff mâœ (â†‘(AlgHom.comp (MvPolynomial.rename â†‘e.symm) (MvPolyno â€¦
           -- ğŸ‰ no goals
    exact hw.1.aevalEquiv.toRingEquiv
    -- ğŸ‰ no goals
  exact IsAlgClosure.equivOfEquiv K L e
  -- ğŸ‰ no goals
#align is_alg_closed.equiv_of_transcendence_basis IsAlgClosed.equivOfTranscendenceBasis

end

end Classification

section Cardinal

variable {R L K : Type u} [CommRing R]

variable [Field K] [Algebra R K] [IsAlgClosed K]

variable {Î¹ : Type u} (v : Î¹ â†’ K)

variable (hv : IsTranscendenceBasis R v)

theorem cardinal_le_max_transcendence_basis (hv : IsTranscendenceBasis R v) :
    #K â‰¤ max (max #R #Î¹) â„µâ‚€ :=
  calc
    #K â‰¤ max #(Algebra.adjoin R (Set.range v)) â„µâ‚€ :=
      letI := isAlgClosure_of_transcendence_basis v hv
      Algebra.IsAlgebraic.cardinal_mk_le_max _ _ IsAlgClosure.algebraic
    _ = max #(MvPolynomial Î¹ R) â„µâ‚€ := by rw [Cardinal.eq.2 âŸ¨hv.1.aevalEquiv.toEquivâŸ©]
                                         -- ğŸ‰ no goals
    _ â‰¤ max (max (max #R #Î¹) â„µâ‚€) â„µâ‚€ := (max_le_max MvPolynomial.cardinal_mk_le_max le_rfl)
    _ = _ := by simp [max_assoc]
                -- ğŸ‰ no goals
#align is_alg_closed.cardinal_le_max_transcendence_basis IsAlgClosed.cardinal_le_max_transcendence_basis

/-- If `K` is an uncountable algebraically closed field, then its
cardinality is the same as that of a transcendence basis. -/
theorem cardinal_eq_cardinal_transcendence_basis_of_aleph0_lt [Nontrivial R]
    (hv : IsTranscendenceBasis R v) (hR : #R â‰¤ â„µâ‚€) (hK : â„µâ‚€ < #K) : #K = #Î¹ :=
  have : â„µâ‚€ â‰¤ #Î¹ := le_of_not_lt fun h => not_le_of_gt hK <|
    calc
      #K â‰¤ max (max #R #Î¹) â„µâ‚€ := cardinal_le_max_transcendence_basis v hv
      _ â‰¤ _ := max_le (max_le hR (le_of_lt h)) le_rfl
  le_antisymm
    (calc
      #K â‰¤ max (max #R #Î¹) â„µâ‚€ := cardinal_le_max_transcendence_basis v hv
      _ = #Î¹ := by
        rw [max_eq_left, max_eq_right]
        -- âŠ¢ #R â‰¤ #Î¹
        Â· exact le_trans hR this
          -- ğŸ‰ no goals
        Â· exact le_max_of_le_right this)
          -- ğŸ‰ no goals
    (mk_le_of_injective (show Function.Injective v from hv.1.injective))
#align is_alg_closed.cardinal_eq_cardinal_transcendence_basis_of_aleph_0_lt IsAlgClosed.cardinal_eq_cardinal_transcendence_basis_of_aleph0_lt

end Cardinal

variable {K L : Type} [Field K] [Field L] [IsAlgClosed K] [IsAlgClosed L]

/-- Two uncountable algebraically closed fields of characteristic zero are isomorphic
if they have the same cardinality. -/
theorem ringEquivOfCardinalEqOfCharZero [CharZero K] [CharZero L] (hK : â„µâ‚€ < #K)
    (hKL : #K = #L) : Nonempty (K â‰ƒ+* L) := by
  cases' exists_isTranscendenceBasis â„¤
    (show Function.Injective (algebraMap â„¤ K) from Int.cast_injective) with s hs
  cases' exists_isTranscendenceBasis â„¤
    (show Function.Injective (algebraMap â„¤ L) from Int.cast_injective) with t ht
  have : #s = #t := by
    rw [â† cardinal_eq_cardinal_transcendence_basis_of_aleph0_lt _ hs (le_of_eq mk_int) hK, â†
      cardinal_eq_cardinal_transcendence_basis_of_aleph0_lt _ ht (le_of_eq mk_int), hKL]
    rwa [â† hKL]
  cases' Cardinal.eq.1 this with e
  -- âŠ¢ Nonempty (K â‰ƒ+* L)
  exact âŸ¨equivOfTranscendenceBasis _ _ e hs htâŸ©
  -- ğŸ‰ no goals
#align is_alg_closed.ring_equiv_of_cardinal_eq_of_char_zero IsAlgClosed.ringEquivOfCardinalEqOfCharZero

private theorem ringEquivOfCardinalEqOfCharP (p : â„•) [Fact p.Prime] [CharP K p] [CharP L p]
    (hK : â„µâ‚€ < #K) (hKL : #K = #L) : Nonempty (K â‰ƒ+* L) := by
  letI : Algebra (ZMod p) K := ZMod.algebra _ _
  -- âŠ¢ Nonempty (K â‰ƒ+* L)
  letI : Algebra (ZMod p) L := ZMod.algebra _ _
  -- âŠ¢ Nonempty (K â‰ƒ+* L)
  cases' exists_isTranscendenceBasis (ZMod p)
    (show Function.Injective (algebraMap (ZMod p) K) from RingHom.injective _) with s hs
  cases' exists_isTranscendenceBasis (ZMod p)
    (show Function.Injective (algebraMap (ZMod p) L) from RingHom.injective _) with t ht
  have : #s = #t := by
    rw [â† cardinal_eq_cardinal_transcendence_basis_of_aleph0_lt _ hs
      (lt_aleph0_of_finite (ZMod p)).le hK,
      â† cardinal_eq_cardinal_transcendence_basis_of_aleph0_lt _ ht
        (lt_aleph0_of_finite (ZMod p)).le, hKL]
    rwa [â† hKL]
  cases' Cardinal.eq.1 this with e
  -- âŠ¢ Nonempty (K â‰ƒ+* L)
  exact âŸ¨equivOfTranscendenceBasis _ _ e hs htâŸ©
  -- ğŸ‰ no goals

/-- Two uncountable algebraically closed fields are isomorphic
if they have the same cardinality and the same characteristic. -/
theorem ringEquivOfCardinalEqOfCharEq (p : â„•) [CharP K p] [CharP L p] (hK : â„µâ‚€ < #K)
    (hKL : #K = #L) : Nonempty (K â‰ƒ+* L) := by
  rcases CharP.char_is_prime_or_zero K p with (hp | hp)
  -- âŠ¢ Nonempty (K â‰ƒ+* L)
  Â· haveI : Fact p.Prime := âŸ¨hpâŸ©
    -- âŠ¢ Nonempty (K â‰ƒ+* L)
    exact ringEquivOfCardinalEqOfCharP p hK hKL
    -- ğŸ‰ no goals
  Â· simp only [hp] at *
    -- âŠ¢ Nonempty (K â‰ƒ+* L)
    letI : CharZero K := CharP.charP_to_charZero K
    -- âŠ¢ Nonempty (K â‰ƒ+* L)
    letI : CharZero L := CharP.charP_to_charZero L
    -- âŠ¢ Nonempty (K â‰ƒ+* L)
    exact ringEquivOfCardinalEqOfCharZero hK hKL
    -- ğŸ‰ no goals
#align is_alg_closed.ring_equiv_of_cardinal_eq_of_char_eq IsAlgClosed.ringEquivOfCardinalEqOfCharEq

end IsAlgClosed
