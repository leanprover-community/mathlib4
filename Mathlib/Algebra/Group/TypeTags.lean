/-
Copyright (c) 2018 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro
-/
import Mathlib.Algebra.Hom.Group
import Mathlib.Logic.Equiv.Defs
import Mathlib.Data.Finite.Defs

#align_import algebra.group.type_tags from "leanprover-community/mathlib"@"2e0975f6a25dd3fbfb9e41556a77f075f6269748"

/-!
# Type tags that turn additive structures into multiplicative, and vice versa

We define two type tags:

* `Additive Œ±`: turns any multiplicative structure on `Œ±` into the corresponding
  additive structure on `Additive Œ±`;
* `Multiplicative Œ±`: turns any additive structure on `Œ±` into the corresponding
  multiplicative structure on `Multiplicative Œ±`.

We also define instances `Additive.*` and `Multiplicative.*` that actually transfer the structures.

## See also

This file is similar to `Order.Synonym`.

## Porting notes

- Since bundled morphism applications that rely on `CoeFun` currently don't work, they are ported
  as `toFoo a` rather than `a.toFoo` for now. (https://github.com/leanprover/lean4/issues/1910)

-/

universe u v

variable {Œ± : Type u} {Œ≤ : Type v}

/-- If `Œ±` carries some multiplicative structure, then `Additive Œ±` carries the corresponding
additive structure. -/
def Additive (Œ± : Type*) := Œ±
#align additive Additive

/-- If `Œ±` carries some additive structure, then `Multiplicative Œ±` carries the corresponding
multiplicative structure. -/
def Multiplicative (Œ± : Type*) := Œ±
#align multiplicative Multiplicative

namespace Additive

/-- Reinterpret `x : Œ±` as an element of `Additive Œ±`. -/
def ofMul : Œ± ‚âÉ Additive Œ± :=
  ‚ü®fun x => x, fun x => x, fun _ => rfl, fun _ => rfl‚ü©
#align additive.of_mul Additive.ofMul

/-- Reinterpret `x : Additive Œ±` as an element of `Œ±`. -/
def toMul : Additive Œ± ‚âÉ Œ± := ofMul.symm
#align additive.to_mul Additive.toMul

@[simp]
theorem ofMul_symm_eq : (@ofMul Œ±).symm = toMul :=
  rfl
#align additive.of_mul_symm_eq Additive.ofMul_symm_eq

@[simp]
theorem toMul_symm_eq : (@toMul Œ±).symm = ofMul :=
  rfl
#align additive.to_mul_symm_eq Additive.toMul_symm_eq

end Additive

namespace Multiplicative

/-- Reinterpret `x : Œ±` as an element of `Multiplicative Œ±`. -/
def ofAdd : Œ± ‚âÉ Multiplicative Œ± :=
  ‚ü®fun x => x, fun x => x, fun _ => rfl, fun _ => rfl‚ü©
#align multiplicative.of_add Multiplicative.ofAdd

/-- Reinterpret `x : Multiplicative Œ±` as an element of `Œ±`. -/
def toAdd : Multiplicative Œ± ‚âÉ Œ± := ofAdd.symm
#align multiplicative.to_add Multiplicative.toAdd

@[simp]
theorem ofAdd_symm_eq : (@ofAdd Œ±).symm = toAdd :=
  rfl
#align multiplicative.of_add_symm_eq Multiplicative.ofAdd_symm_eq

@[simp]
theorem toAdd_symm_eq : (@toAdd Œ±).symm = ofAdd :=
  rfl
#align multiplicative.to_add_symm_eq Multiplicative.toAdd_symm_eq

end Multiplicative

open Additive (ofMul toMul)
open Multiplicative (ofAdd toAdd)

@[simp]
theorem toAdd_ofAdd (x : Œ±) : toAdd (ofAdd x) = x :=
  rfl
#align to_add_of_add toAdd_ofAdd

@[simp]
theorem ofAdd_toAdd (x : Multiplicative Œ±) : ofAdd (toAdd x) = x :=
  rfl
#align of_add_to_add ofAdd_toAdd

@[simp]
theorem toMul_ofMul (x : Œ±) : toMul (ofMul x) = x :=
  rfl
#align to_mul_of_mul toMul_ofMul

@[simp]
theorem ofMul_toMul (x : Additive Œ±) : ofMul (toMul x) = x :=
  rfl
#align of_mul_to_mul ofMul_toMul

instance [Inhabited Œ±] : Inhabited (Additive Œ±) :=
  ‚ü®ofMul default‚ü©

instance [Inhabited Œ±] : Inhabited (Multiplicative Œ±) :=
  ‚ü®ofAdd default‚ü©

instance [Finite Œ±] : Finite (Additive Œ±) :=
  Finite.of_equiv Œ± (by rfl)
                        -- üéâ no goals

instance [Finite Œ±] : Finite (Multiplicative Œ±) :=
  Finite.of_equiv Œ± (by rfl)
                        -- üéâ no goals

instance [h: Infinite Œ±] : Infinite (Additive Œ±) := h

instance [h: Infinite Œ±] : Infinite (Multiplicative Œ±) := h

instance instNontrivialAdditive [Nontrivial Œ±] : Nontrivial (Additive Œ±) :=
  ofMul.injective.nontrivial
#align additive.nontrivial instNontrivialAdditive

instance instNontrivialMultiplicative [Nontrivial Œ±] : Nontrivial (Multiplicative Œ±) :=
  ofAdd.injective.nontrivial
#align multiplicative.nontrivial instNontrivialMultiplicative

instance Additive.add [Mul Œ±] : Add (Additive Œ±) where
  add x y := ofMul (toMul x * toMul y)

instance Multiplicative.mul [Add Œ±] : Mul (Multiplicative Œ±) where
  mul x y := ofAdd (toAdd x + toAdd y)

@[simp]
theorem ofAdd_add [Add Œ±] (x y : Œ±) : ofAdd (x + y) = ofAdd x * ofAdd y := rfl
#align of_add_add ofAdd_add

@[simp]
theorem toAdd_mul [Add Œ±] (x y : Multiplicative Œ±) : toAdd (x * y) = toAdd x + toAdd y := rfl
#align to_add_mul toAdd_mul

@[simp]
theorem ofMul_mul [Mul Œ±] (x y : Œ±) : ofMul (x * y) = ofMul x + ofMul y := rfl
#align of_mul_mul ofMul_mul

@[simp]
theorem toMul_add [Mul Œ±] (x y : Additive Œ±) : toMul (x + y) = toMul x * toMul y := rfl
#align to_mul_add toMul_add

instance Additive.addSemigroup [Semigroup Œ±] : AddSemigroup (Additive Œ±) :=
  { Additive.add with add_assoc := @mul_assoc Œ± _ }

instance Multiplicative.semigroup [AddSemigroup Œ±] : Semigroup (Multiplicative Œ±) :=
  { Multiplicative.mul with mul_assoc := @add_assoc Œ± _ }

instance Additive.addCommSemigroup [CommSemigroup Œ±] : AddCommSemigroup (Additive Œ±) :=
  { Additive.addSemigroup with add_comm := @mul_comm Œ± _ }

instance Multiplicative.commSemigroup [AddCommSemigroup Œ±] : CommSemigroup (Multiplicative Œ±) :=
  { Multiplicative.semigroup with mul_comm := @add_comm Œ± _ }

instance Additive.isLeftCancelAdd [Mul Œ±] [IsLeftCancelMul Œ±] : IsLeftCancelAdd (Additive Œ±) :=
  ‚ü®@mul_left_cancel Œ± _ _‚ü©

instance Multiplicative.isLeftCancelMul [Add Œ±] [IsLeftCancelAdd Œ±] :
    IsLeftCancelMul (Multiplicative Œ±) :=
  ‚ü®@add_left_cancel Œ± _ _‚ü©

instance Additive.isRightCancelAdd [Mul Œ±] [IsRightCancelMul Œ±] : IsRightCancelAdd (Additive Œ±) :=
  ‚ü®@mul_right_cancel Œ± _ _‚ü©

instance Multiplicative.isRightCancelMul [Add Œ±] [IsRightCancelAdd Œ±] :
    IsRightCancelMul (Multiplicative Œ±) :=
  ‚ü®@add_right_cancel Œ± _ _‚ü©

instance Additive.isCancelAdd [Mul Œ±] [IsCancelMul Œ±] : IsCancelAdd (Additive Œ±) :=
  ‚ü®‚ü©

instance Multiplicative.isCancelMul [Add Œ±] [IsCancelAdd Œ±] : IsCancelMul (Multiplicative Œ±) :=
  ‚ü®‚ü©

instance Additive.addLeftCancelSemigroup [LeftCancelSemigroup Œ±] :
    AddLeftCancelSemigroup (Additive Œ±) :=
  { Additive.addSemigroup, Additive.isLeftCancelAdd with }

instance Multiplicative.leftCancelSemigroup [AddLeftCancelSemigroup Œ±] :
    LeftCancelSemigroup (Multiplicative Œ±) :=
  { Multiplicative.semigroup, Multiplicative.isLeftCancelMul with }

instance Additive.addRightCancelSemigroup [RightCancelSemigroup Œ±] :
    AddRightCancelSemigroup (Additive Œ±) :=
  { Additive.addSemigroup, Additive.isRightCancelAdd with }

instance Multiplicative.rightCancelSemigroup [AddRightCancelSemigroup Œ±] :
    RightCancelSemigroup (Multiplicative Œ±) :=
  { Multiplicative.semigroup, Multiplicative.isRightCancelMul with }

instance [One Œ±] : Zero (Additive Œ±) :=
  ‚ü®Additive.ofMul 1‚ü©

@[simp]
theorem ofMul_one [One Œ±] : @Additive.ofMul Œ± 1 = 0 := rfl
#align of_mul_one ofMul_one

@[simp]
theorem ofMul_eq_zero {A : Type*} [One A] {x : A} : Additive.ofMul x = 0 ‚Üî x = 1 := Iff.rfl
#align of_mul_eq_zero ofMul_eq_zero

@[simp]
theorem toMul_zero [One Œ±] : toMul (0 : Additive Œ±) = 1 := rfl
#align to_mul_zero toMul_zero

instance [Zero Œ±] : One (Multiplicative Œ±) :=
  ‚ü®Multiplicative.ofAdd 0‚ü©

@[simp]
theorem ofAdd_zero [Zero Œ±] : @Multiplicative.ofAdd Œ± 0 = 1 :=
  rfl
#align of_add_zero ofAdd_zero

@[simp]
theorem ofAdd_eq_one {A : Type*} [Zero A] {x : A} : Multiplicative.ofAdd x = 1 ‚Üî x = 0 :=
  Iff.rfl
#align of_add_eq_one ofAdd_eq_one

@[simp]
theorem toAdd_one [Zero Œ±] : toAdd (1 : Multiplicative Œ±) = 0 :=
  rfl
#align to_add_one toAdd_one

instance Additive.addZeroClass [MulOneClass Œ±] : AddZeroClass (Additive Œ±) where
  zero := 0
  add := (¬∑ + ¬∑)
  zero_add := @one_mul Œ± _
  add_zero := @mul_one Œ± _

instance Multiplicative.mulOneClass [AddZeroClass Œ±] : MulOneClass (Multiplicative Œ±) where
  one := 1
  mul := (¬∑ * ¬∑)
  one_mul := @zero_add Œ± _
  mul_one := @add_zero Œ± _

instance Additive.addMonoid [h : Monoid Œ±] : AddMonoid (Additive Œ±) :=
  { Additive.addZeroClass, Additive.addSemigroup with
    zero := 0
    add := (¬∑ + ¬∑)
    nsmul := @Monoid.npow Œ± h
    nsmul_zero := @Monoid.npow_zero Œ± h
    nsmul_succ := @Monoid.npow_succ Œ± h }

instance Multiplicative.monoid [h : AddMonoid Œ±] : Monoid (Multiplicative Œ±) :=
  { Multiplicative.mulOneClass, Multiplicative.semigroup with
    one := 1
    mul := (¬∑ * ¬∑)
    npow := @AddMonoid.nsmul Œ± h
    npow_zero := @AddMonoid.nsmul_zero Œ± h
    npow_succ := @AddMonoid.nsmul_succ Œ± h }

instance Additive.addLeftCancelMonoid [LeftCancelMonoid Œ±] : AddLeftCancelMonoid (Additive Œ±) :=
  { Additive.addMonoid, Additive.addLeftCancelSemigroup with zero := 0, add := (¬∑ + ¬∑) }

instance Multiplicative.leftCancelMonoid [AddLeftCancelMonoid Œ±] :
    LeftCancelMonoid (Multiplicative Œ±) :=
  { Multiplicative.monoid, Multiplicative.leftCancelSemigroup with one := 1, mul := (¬∑ * ¬∑) }

instance Additive.addRightCancelMonoid [RightCancelMonoid Œ±] : AddRightCancelMonoid (Additive Œ±) :=
  { Additive.addMonoid, Additive.addRightCancelSemigroup with zero := 0, add := (¬∑ + ¬∑) }

instance Multiplicative.rightCancelMonoid [AddRightCancelMonoid Œ±] :
    RightCancelMonoid (Multiplicative Œ±) :=
  { Multiplicative.monoid, Multiplicative.rightCancelSemigroup with one := 1, mul := (¬∑ * ¬∑) }

instance Additive.addCommMonoid [CommMonoid Œ±] : AddCommMonoid (Additive Œ±) :=
  { Additive.addMonoid, Additive.addCommSemigroup with zero := 0, add := (¬∑ + ¬∑) }

instance Multiplicative.commMonoid [AddCommMonoid Œ±] : CommMonoid (Multiplicative Œ±) :=
  { Multiplicative.monoid, Multiplicative.commSemigroup with one := 1, mul := (¬∑ * ¬∑) }

instance Additive.neg [Inv Œ±] : Neg (Additive Œ±) :=
  ‚ü®fun x => ofAdd (toMul x)‚Åª¬π‚ü©

@[simp]
theorem ofMul_inv [Inv Œ±] (x : Œ±) : ofMul x‚Åª¬π = -ofMul x :=
  rfl
#align of_mul_inv ofMul_inv

@[simp]
theorem toMul_neg [Inv Œ±] (x : Additive Œ±) : toMul (-x) = (toMul x)‚Åª¬π :=
  rfl
#align to_mul_neg toMul_neg

instance Multiplicative.inv [Neg Œ±] : Inv (Multiplicative Œ±) :=
  ‚ü®fun x => ofMul (-toAdd x)‚ü©

@[simp]
theorem ofAdd_neg [Neg Œ±] (x : Œ±) : ofAdd (-x) = (ofAdd x)‚Åª¬π :=
  rfl
#align of_add_neg ofAdd_neg

@[simp]
theorem toAdd_inv [Neg Œ±] (x : Multiplicative Œ±) : toAdd x‚Åª¬π = -(toAdd x) :=
  rfl
#align to_add_inv toAdd_inv

instance Additive.sub [Div Œ±] : Sub (Additive Œ±) where
  sub x y := ofMul (toMul x / toMul y)
#align additive.has_sub Additive.sub

instance Multiplicative.div [Sub Œ±] : Div (Multiplicative Œ±) where
  div x y := ofAdd (toAdd x - toAdd y)
#align multiplicative.has_div Multiplicative.div

@[simp]
theorem ofAdd_sub [Sub Œ±] (x y : Œ±) : ofAdd (x - y) = ofAdd x / ofAdd y :=
  rfl
#align of_add_sub ofAdd_sub

@[simp]
theorem toAdd_div [Sub Œ±] (x y : Multiplicative Œ±) : toAdd (x / y) = toAdd x - toAdd y :=
  rfl
#align to_add_div toAdd_div

@[simp]
theorem ofMul_div [Div Œ±] (x y : Œ±) : ofMul (x / y) = ofMul x - ofMul y :=
  rfl
#align of_mul_div ofMul_div

@[simp]
theorem toMul_sub [Div Œ±] (x y : Additive Œ±) : toMul (x - y) = toMul x / toMul y :=
  rfl
#align to_mul_sub toMul_sub

instance Additive.involutiveNeg [InvolutiveInv Œ±] : InvolutiveNeg (Additive Œ±) :=
  { Additive.neg with neg_neg := @inv_inv Œ± _ }

instance Multiplicative.involutiveInv [InvolutiveNeg Œ±] : InvolutiveInv (Multiplicative Œ±) :=
  { Multiplicative.inv with inv_inv := @neg_neg Œ± _ }

instance Additive.subNegMonoid [DivInvMonoid Œ±] : SubNegMonoid (Additive Œ±) :=
  { Additive.neg, Additive.sub, Additive.addMonoid with
    sub_eq_add_neg := @div_eq_mul_inv Œ± _
    zsmul := @DivInvMonoid.zpow Œ± _
    zsmul_zero' := @DivInvMonoid.zpow_zero' Œ± _
    zsmul_succ' := @DivInvMonoid.zpow_succ' Œ± _
    zsmul_neg' := @DivInvMonoid.zpow_neg' Œ± _ }

instance Multiplicative.divInvMonoid [SubNegMonoid Œ±] : DivInvMonoid (Multiplicative Œ±) :=
  { Multiplicative.inv, Multiplicative.div, Multiplicative.monoid with
    div_eq_mul_inv := @sub_eq_add_neg Œ± _
    zpow := @SubNegMonoid.zsmul Œ± _
    zpow_zero' := @SubNegMonoid.zsmul_zero' Œ± _
    zpow_succ' := @SubNegMonoid.zsmul_succ' Œ± _
    zpow_neg' := @SubNegMonoid.zsmul_neg' Œ± _ }

instance Additive.subtractionMonoid [DivisionMonoid Œ±] : SubtractionMonoid (Additive Œ±) :=
  { Additive.subNegMonoid, Additive.involutiveNeg with
    neg_add_rev := @mul_inv_rev Œ± _
    neg_eq_of_add := @inv_eq_of_mul_eq_one_right Œ± _ }

instance Multiplicative.divisionMonoid [SubtractionMonoid Œ±] : DivisionMonoid (Multiplicative Œ±) :=
  { Multiplicative.divInvMonoid, Multiplicative.involutiveInv with
    mul_inv_rev := @neg_add_rev Œ± _
    inv_eq_of_mul := @neg_eq_of_add_eq_zero_right Œ± _ }

instance Additive.subtractionCommMonoid [DivisionCommMonoid Œ±] :
    SubtractionCommMonoid (Additive Œ±) :=
  { Additive.subtractionMonoid, Additive.addCommSemigroup with }

instance Multiplicative.divisionCommMonoid [SubtractionCommMonoid Œ±] :
    DivisionCommMonoid (Multiplicative Œ±) :=
  { Multiplicative.divisionMonoid, Multiplicative.commSemigroup with }

instance Additive.addGroup [Group Œ±] : AddGroup (Additive Œ±) :=
  { Additive.subNegMonoid with add_left_neg := @mul_left_inv Œ± _ }

instance Multiplicative.group [AddGroup Œ±] : Group (Multiplicative Œ±) :=
  { Multiplicative.divInvMonoid with mul_left_inv := @add_left_neg Œ± _ }

instance Additive.addCommGroup [CommGroup Œ±] : AddCommGroup (Additive Œ±) :=
  { Additive.addGroup, Additive.addCommMonoid with }

instance Multiplicative.commGroup [AddCommGroup Œ±] : CommGroup (Multiplicative Œ±) :=
  { Multiplicative.group, Multiplicative.commMonoid with }

/-- Reinterpret `Œ± ‚Üí+ Œ≤` as `Multiplicative Œ± ‚Üí* Multiplicative Œ≤`. -/
@[simps]
def AddMonoidHom.toMultiplicative [AddZeroClass Œ±] [AddZeroClass Œ≤] :
    (Œ± ‚Üí+ Œ≤) ‚âÉ (Multiplicative Œ± ‚Üí* Multiplicative Œ≤) where
  toFun f := {
    toFun := fun a => ofAdd (f (toAdd a))
    map_mul' := f.map_add
    map_one' := f.map_zero
  }
  invFun f := {
    toFun := fun a => toAdd (f (ofAdd a))
    map_add' := f.map_mul
    map_zero' := f.map_one
  }
  left_inv _ := rfl
  right_inv _ := rfl
#align add_monoid_hom.to_multiplicative AddMonoidHom.toMultiplicative
#align add_monoid_hom.to_multiplicative_symm_apply_apply AddMonoidHom.toMultiplicative_symm_apply_apply
#align add_monoid_hom.to_multiplicative_apply_apply AddMonoidHom.toMultiplicative_apply_apply

/-- Reinterpret `Œ± ‚Üí* Œ≤` as `Additive Œ± ‚Üí+ Additive Œ≤`. -/
@[simps]
def MonoidHom.toAdditive [MulOneClass Œ±] [MulOneClass Œ≤] :
    (Œ± ‚Üí* Œ≤) ‚âÉ (Additive Œ± ‚Üí+ Additive Œ≤) where
  toFun f := {
    toFun := fun a => ofMul (f (toMul a))
    map_add' := f.map_mul
    map_zero' := f.map_one
  }
  invFun f := {
    toFun := fun a => toMul (f (ofMul a))
    map_mul' := f.map_add
    map_one' := f.map_zero
  }
  left_inv _ := rfl
  right_inv _ := rfl
#align monoid_hom.to_additive MonoidHom.toAdditive
#align monoid_hom.to_additive_symm_apply_apply MonoidHom.toAdditive_symm_apply_apply
#align monoid_hom.to_additive_apply_apply MonoidHom.toAdditive_apply_apply

/-- Reinterpret `Additive Œ± ‚Üí+ Œ≤` as `Œ± ‚Üí* Multiplicative Œ≤`. -/
@[simps]
def AddMonoidHom.toMultiplicative' [MulOneClass Œ±] [AddZeroClass Œ≤] :
    (Additive Œ± ‚Üí+ Œ≤) ‚âÉ (Œ± ‚Üí* Multiplicative Œ≤) where
  toFun f := {
    toFun := fun a => ofAdd (f (ofMul a))
    map_mul' := f.map_add
    map_one' := f.map_zero
  }
  invFun f := {
    toFun := fun a => toAdd (f (toMul a))
    map_add' := f.map_mul
    map_zero' := f.map_one
  }
  left_inv _ := rfl
  right_inv _ := rfl
#align add_monoid_hom.to_multiplicative' AddMonoidHom.toMultiplicative'
#align add_monoid_hom.to_multiplicative'_apply_apply AddMonoidHom.toMultiplicative'_apply_apply
#align add_monoid_hom.to_multiplicative'_symm_apply_apply AddMonoidHom.toMultiplicative'_symm_apply_apply

/-- Reinterpret `Œ± ‚Üí* Multiplicative Œ≤` as `Additive Œ± ‚Üí+ Œ≤`. -/
@[simps!]
def MonoidHom.toAdditive' [MulOneClass Œ±] [AddZeroClass Œ≤] :
    (Œ± ‚Üí* Multiplicative Œ≤) ‚âÉ (Additive Œ± ‚Üí+ Œ≤) :=
  AddMonoidHom.toMultiplicative'.symm
#align monoid_hom.to_additive' MonoidHom.toAdditive'
#align monoid_hom.to_additive'_symm_apply_apply MonoidHom.toAdditive'_symm_apply_apply
#align monoid_hom.to_additive'_apply_apply MonoidHom.toAdditive'_apply_apply

/-- Reinterpret `Œ± ‚Üí+ Additive Œ≤` as `Multiplicative Œ± ‚Üí* Œ≤`. -/
@[simps]
def AddMonoidHom.toMultiplicative'' [AddZeroClass Œ±] [MulOneClass Œ≤] :
    (Œ± ‚Üí+ Additive Œ≤) ‚âÉ (Multiplicative Œ± ‚Üí* Œ≤) where
  toFun f := {
    toFun := fun a => toMul (f (toAdd a))
    map_mul' := f.map_add
    map_one' := f.map_zero
  }
  invFun f := {
    toFun := fun a => ofMul (f (ofAdd a))
    map_add' := f.map_mul
    map_zero' := f.map_one
  }
  left_inv _ := rfl
  right_inv _ := rfl
#align add_monoid_hom.to_multiplicative'' AddMonoidHom.toMultiplicative''
#align add_monoid_hom.to_multiplicative''_symm_apply_apply AddMonoidHom.toMultiplicative''_symm_apply_apply
#align add_monoid_hom.to_multiplicative''_apply_apply AddMonoidHom.toMultiplicative''_apply_apply

/-- Reinterpret `Multiplicative Œ± ‚Üí* Œ≤` as `Œ± ‚Üí+ Additive Œ≤`. -/
@[simps!]
def MonoidHom.toAdditive'' [AddZeroClass Œ±] [MulOneClass Œ≤] :
    (Multiplicative Œ± ‚Üí* Œ≤) ‚âÉ (Œ± ‚Üí+ Additive Œ≤) :=
  AddMonoidHom.toMultiplicative''.symm
#align monoid_hom.to_additive'' MonoidHom.toAdditive''
#align monoid_hom.to_additive''_symm_apply_apply MonoidHom.toAdditive''_symm_apply_apply
#align monoid_hom.to_additive''_apply_apply MonoidHom.toAdditive''_apply_apply

/-- If `Œ±` has some multiplicative structure and coerces to a function,
then `Additive Œ±` should also coerce to the same function.

This allows `Additive` to be used on bundled function types with a multiplicative structure, which
is often used for composition, without affecting the behavior of the function itself.
-/
instance Additive.coeToFun {Œ± : Type*} {Œ≤ : Œ± ‚Üí Sort*} [CoeFun Œ± Œ≤] :
    CoeFun (Additive Œ±) fun a => Œ≤ (toMul a) :=
  ‚ü®fun a => CoeFun.coe (toMul a)‚ü©
#align additive.has_coe_to_fun Additive.coeToFun

/-- If `Œ±` has some additive structure and coerces to a function,
then `Multiplicative Œ±` should also coerce to the same function.

This allows `Multiplicative` to be used on bundled function types with an additive structure, which
is often used for composition, without affecting the behavior of the function itself.
-/
instance Multiplicative.coeToFun {Œ± : Type*} {Œ≤ : Œ± ‚Üí Sort*} [CoeFun Œ± Œ≤] :
    CoeFun (Multiplicative Œ±) fun a => Œ≤ (toAdd a) :=
  ‚ü®fun a => CoeFun.coe (toAdd a)‚ü©
#align multiplicative.has_coe_to_fun Multiplicative.coeToFun
