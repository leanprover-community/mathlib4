/-
Copyright (c) 2023 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes
-/

import Mathlib.Algebra.Group.Defs

/-!
# Minimal Axioms for a Group

This file defines constructors to define a group structure on a Type, while proving only three
equalities.

## Main Definitions

* `Group.ofLeftAxioms`: Define a group structure on a Type by proving `âˆ€ a, 1 * a = a` and
  `âˆ€ a, aâ»Â¹ * a = 1` and associativity.
* `Group.ofRightAxioms`: Define a group structure on a Type by proving `âˆ€ a, a * 1 = a` and
  `âˆ€ a, a * aâ»Â¹ = 1` and associativity.

-/

set_option autoImplicit true

/-- Define a `Group` structure on a Type by proving `âˆ€ a, 1 * a = a` and
`âˆ€ a, aâ»Â¹ * a = 1`.
Note that this uses the default definitions for `npow`, `zpow` and `div`.
See note [reducible non-instances]. -/
@[to_additive (attr := reducible)
"Define an `AddGroup` structure on a Type by proving `âˆ€ a, 0 + a = a` and
`âˆ€ a, -a + a = 0`.
Note that this uses the default definitions for `nsmul`, `zsmul` and `sub`.
See note [reducible non-instances]."]
def Group.ofLeftAxioms {G : Type u} [Mul G] [Inv G] [One G]
    (assoc : âˆ€ a b c : G, (a * b) * c = a * (b * c))
    (one_mul : âˆ€ a : G, 1 * a = a)
    (mul_left_inv : âˆ€ a : G, aâ»Â¹ * a = 1) : Group G :=
  { mul_assoc := assoc,
    one_mul := one_mul,
    mul_left_inv := mul_left_inv,
    mul_one := fun a => by
      have mul_right_inv : âˆ€ a, a * aâ»Â¹ = 1 := fun a =>
        calc a * aâ»Â¹ = 1 * (a * aâ»Â¹) := (one_mul _).symm
          _ = ((a * aâ»Â¹)â»Â¹ * (a * aâ»Â¹)) * (a * aâ»Â¹) := by
            rw [mul_left_inv]
          _ = (a * aâ»Â¹)â»Â¹ * (a * ((aâ»Â¹ * a) * aâ»Â¹)) := by
            simp only [assoc]
          _ = 1 := by
            rw [mul_left_inv, one_mul, mul_left_inv]
      rw [â† mul_left_inv a, â† assoc, mul_right_inv a, one_mul] }
      -- ğŸ‰ no goals

/-- Define a `Group` structure on a Type by proving `âˆ€ a, a * 1 = a` and
`âˆ€ a, a * aâ»Â¹ = 1`.
Note that this uses the default definitions for `npow`, `zpow` and `div`.
See note [reducible non-instances]. -/
@[to_additive (attr := reducible)
"Define an `AddGroup` structure on a Type by proving `âˆ€ a, a + 0 = a` and
`âˆ€ a, a + -a = 0`.
Note that this uses the default definitions for `nsmul`, `zsmul` and `sub`.
See note [reducible non-instances]."]
def Group.ofRightAxioms {G : Type u} [Mul G] [Inv G] [One G]
    (assoc : âˆ€ a b c : G, (a * b) * c = a * (b * c))
    (mul_one : âˆ€ a : G, a * 1 = a)
    (mul_right_inv : âˆ€ a : G, a * aâ»Â¹ = 1) : Group G :=
  have mul_left_inv : âˆ€ a, aâ»Â¹ * a = 1 := fun a =>
    calc aâ»Â¹ * a = (aâ»Â¹ * a) * 1 := (mul_one _).symm
      _ = (aâ»Â¹ * a) * ((aâ»Â¹ * a) * (aâ»Â¹ * a)â»Â¹) := by
        rw [mul_right_inv]
        -- ğŸ‰ no goals
      _ = ((aâ»Â¹ * (a * aâ»Â¹)) * a) * (aâ»Â¹ * a)â»Â¹ := by
        simp only [assoc]
        -- ğŸ‰ no goals
      _ = 1 := by
        rw [mul_right_inv, mul_one, mul_right_inv]
        -- ğŸ‰ no goals
  { mul_assoc := assoc,
    mul_one := mul_one,
    mul_left_inv := mul_left_inv,
    one_mul := fun a => by
      rw [â† mul_right_inv a, assoc, mul_left_inv, mul_one] }
      -- ğŸ‰ no goals
