/-
Copyright (c) 2022 Damiano Testa. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Damiano Testa
-/
import Mathlib.Data.Finset.Preimage

#align_import algebra.group.unique_prods from "leanprover-community/mathlib"@"d6fad0e5bf2d6f48da9175d25c3dc5706b3834ce"

/-!
# Unique products and related notions

A group `G` has *unique products* if for any two non-empty finite subsets `A, B ‚äÇ G`, there is an
element `g ‚àà A * B` that can be written uniquely as a product of an element of `A` and an element
of `B`.  We call the formalization this property `UniqueProds`.  Since the condition requires no
property of the group operation, we define it for a Type simply satisfying `Mul`.  We also
introduce the analogous "additive" companion, `UniqueSums`, and link the two so that `to_additive`
converts `UniqueProds` into `UniqueSums`.

A common way of *proving* that a group satisfies the `UniqueProds/Sums` property is by assuming
the existence of some kind of ordering on the group that is well-behaved with respect to the
group operation and showing that minima/maxima are the "unique products/sums".
However, the order is just a convenience and is not part of the `UniqueProds/Sums` setup.

Here you can see several examples of Types that have `UniqueSums/Prods`
(`inferInstance` uses `Covariant.to_uniqueProds_left` and `Covariant.to_uniqueSums_left`).
```lean
import Mathlib.Data.Real.Basic
import Mathlib.Data.PNat.Basic
import Mathlib.Algebra.Group.UniqueProds

example : UniqueSums ‚Ñï   := inferInstance
example : UniqueSums ‚Ñï+  := inferInstance
example : UniqueSums ‚Ñ§   := inferInstance
example : UniqueSums ‚Ñö   := inferInstance
example : UniqueSums ‚Ñù   := inferInstance
example : UniqueProds ‚Ñï+ := inferInstance
```

## Use in `(Add)MonoidAlgebra`s

`UniqueProds/Sums` allow to decouple certain arguments about `(Add)MonoidAlgebra`s into an argument
about the grading type and then a generic statement of the form "look at the coefficient of the
'unique product/sum'".
The file `Algebra/MonoidAlgebra/NoZeroDivisors` contains several examples of this use.
-/


/-- Let `G` be a Type with multiplication, let `A B : Finset G` be finite subsets and
let `a0 b0 : G` be two elements.  `UniqueMul A B a0 b0` asserts `a0 * b0` can be written in at
most one way as a product of an element of `A` and an element of `B`. -/
@[to_additive
      "Let `G` be a Type with addition, let `A B : Finset G` be finite subsets and
let `a0 b0 : G` be two elements.  `UniqueAdd A B a0 b0` asserts `a0 + b0` can be written in at
most one way as a sum of an element from `A` and an element from `B`."]
def UniqueMul {G} [Mul G] (A B : Finset G) (a0 b0 : G) : Prop :=
  ‚àÄ ‚¶Éa b‚¶Ñ, a ‚àà A ‚Üí b ‚àà B ‚Üí a * b = a0 * b0 ‚Üí a = a0 ‚àß b = b0
#align unique_mul UniqueMul
#align unique_add UniqueAdd

namespace UniqueMul

variable {G H : Type*} [Mul G] [Mul H] {A B : Finset G} {a0 b0 : G}

theorem mt {G} [Mul G] {A B : Finset G} {a0 b0 : G} (h : UniqueMul A B a0 b0) :
    ‚àÄ ‚¶Éa b‚¶Ñ, a ‚àà A ‚Üí b ‚àà B ‚Üí a ‚â† a0 ‚à® b ‚â† b0 ‚Üí a * b ‚â† a0 * b0 := fun _ _ ha hb k ‚Ü¶ by
  contrapose! k
  -- ‚ä¢ x‚úù¬π = a0 ‚àß x‚úù = b0
  exact h ha hb k
  -- üéâ no goals
#align unique_mul.mt UniqueMul.mt

@[to_additive]
theorem subsingleton (A B : Finset G) (a0 b0 : G) (h : UniqueMul A B a0 b0) :
    Subsingleton { ab : G √ó G // ab.1 ‚àà A ‚àß ab.2 ‚àà B ‚àß ab.1 * ab.2 = a0 * b0 } :=
  ‚ü®fun ‚ü®‚ü®_a, _b‚ü©, ha, hb, ab‚ü© ‚ü®‚ü®_a', _b'‚ü©, ha', hb', ab'‚ü© ‚Ü¶
    Subtype.ext <|
      Prod.ext ((h ha hb ab).1.trans (h ha' hb' ab').1.symm) <|
        (h ha hb ab).2.trans (h ha' hb' ab').2.symm‚ü©
#align unique_mul.subsingleton UniqueMul.subsingleton
#align unique_add.subsingleton UniqueAdd.subsingleton

@[to_additive]
theorem set_subsingleton (A B : Finset G) (a0 b0 : G) (h : UniqueMul A B a0 b0) :
    Set.Subsingleton { ab : G √ó G | ab.1 ‚àà A ‚àß ab.2 ‚àà B ‚àß ab.1 * ab.2 = a0 * b0 } := by
  rintro ‚ü®x1, y1‚ü© (hx : x1 ‚àà A ‚àß y1 ‚àà B ‚àß x1 * y1 = a0 * b0) ‚ü®x2, y2‚ü©
    (hy : x2 ‚àà A ‚àß y2 ‚àà B ‚àß x2 * y2 = a0 * b0)
  rcases h hx.1 hx.2.1 hx.2.2 with ‚ü®rfl, rfl‚ü©
  -- ‚ä¢ (x1, y1) = (x2, y2)
  rcases h hy.1 hy.2.1 hy.2.2 with ‚ü®rfl, rfl‚ü©
  -- ‚ä¢ (x2, y2) = (x2, y2)
  rfl
  -- üéâ no goals
#align unique_mul.set_subsingleton UniqueMul.set_subsingleton
#align unique_add.set_subsingleton UniqueAdd.set_subsingleton

-- Porting note: mathport warning: expanding binder collection
--  (ab ¬´expr ‚àà ¬ª [finset.product/multiset.product/set.prod/list.product](A, B)) -/
@[to_additive]
theorem iff_existsUnique (aA : a0 ‚àà A) (bB : b0 ‚àà B) :
    UniqueMul A B a0 b0 ‚Üî ‚àÉ! (ab : _) (_ : ab ‚àà A √óÀ¢ B), ab.1 * ab.2 = a0 * b0 :=
  ‚ü®fun _ ‚Ü¶ ‚ü®(a0, b0), ‚ü®Finset.mem_product.mpr ‚ü®aA, bB‚ü©, rfl, by simp‚ü©, by simpa‚ü©,
                                                                -- üéâ no goals
                                                                          -- üéâ no goals
    fun h ‚Ü¶ h.elim‚ÇÇ
      (by
        rintro ‚ü®x1, x2‚ü© _ _ J x y hx hy l
        -- ‚ä¢ x = a0 ‚àß y = b0
        rcases Prod.mk.inj_iff.mp (J (a0, b0) (Finset.mk_mem_product aA bB) rfl) with ‚ü®rfl, rfl‚ü©
        -- ‚ä¢ x = a0 ‚àß y = b0
        exact Prod.mk.inj_iff.mp (J (x, y) (Finset.mk_mem_product hx hy) l))‚ü©
        -- üéâ no goals
#align unique_mul.iff_exists_unique UniqueMul.iff_existsUnique
#align unique_add.iff_exists_unique UniqueAdd.iff_existsUnique

-- Porting note: mathport warning: expanding binder collection
--  (ab ¬´expr ‚àà ¬ª [finset.product/multiset.product/set.prod/list.product](A, B)) -/
@[to_additive]
theorem exists_iff_exists_existsUnique :
    (‚àÉ a0 b0 : G, a0 ‚àà A ‚àß b0 ‚àà B ‚àß UniqueMul A B a0 b0) ‚Üî
      ‚àÉ g : G, ‚àÉ! (ab : _) (_ : ab ‚àà A √óÀ¢ B), ab.1 * ab.2 = g :=
  ‚ü®fun ‚ü®a0, b0, hA, hB, h‚ü© ‚Ü¶ ‚ü®_, (iff_existsUnique hA hB).mp h‚ü©, fun ‚ü®g, h‚ü© ‚Ü¶ by
    have h' := h
    -- ‚ä¢ ‚àÉ a0 b0, a0 ‚àà A ‚àß b0 ‚àà B ‚àß UniqueMul A B a0 b0
    rcases h' with ‚ü®‚ü®a, b‚ü©, ‚ü®hab, rfl, -‚ü©, -‚ü©
    -- ‚ä¢ ‚àÉ a0 b0, a0 ‚àà A ‚àß b0 ‚àà B ‚àß UniqueMul A B a0 b0
    cases' Finset.mem_product.mp hab with ha hb
    -- ‚ä¢ ‚àÉ a0 b0, a0 ‚àà A ‚àß b0 ‚àà B ‚àß UniqueMul A B a0 b0
    exact ‚ü®a, b, ha, hb, (iff_existsUnique ha hb).mpr h‚ü©‚ü©
    -- üéâ no goals
#align unique_mul.exists_iff_exists_exists_unique UniqueMul.exists_iff_exists_existsUnique
#align unique_add.exists_iff_exists_exists_unique UniqueAdd.exists_iff_exists_existsUnique

/-- `UniqueMul` is preserved by inverse images under injective, multiplicative maps. -/
@[to_additive "`UniqueAdd` is preserved by inverse images under injective, additive maps."]
theorem mulHom_preimage (f : G ‚Üí‚Çô* H) (hf : Function.Injective f) (a0 b0 : G) {A B : Finset H}
    (u : UniqueMul A B (f a0) (f b0)) :
    UniqueMul (A.preimage f (Set.injOn_of_injective hf _))
      (B.preimage f (Set.injOn_of_injective hf _)) a0 b0 := by
  intro a b ha hb ab
  -- ‚ä¢ a = a0 ‚àß b = b0
  simp only [‚Üê hf.eq_iff, map_mul] at ab ‚ä¢
  -- ‚ä¢ ‚Üëf a = ‚Üëf a0 ‚àß ‚Üëf b = ‚Üëf b0
  exact u (Finset.mem_preimage.mp ha) (Finset.mem_preimage.mp hb) ab
  -- üéâ no goals
#align unique_mul.mul_hom_preimage UniqueMul.mulHom_preimage
#align unique_add.add_hom_preimage UniqueAdd.addHom_preimage

/-- `Unique_Mul` is preserved under multiplicative maps that are injective.

See `UniqueMul.mulHom_map_iff` for a version with swapped bundling. -/
@[to_additive
      "`UniqueAdd` is preserved under additive maps that are injective.

See `UniqueAdd.addHom_map_iff` for a version with swapped bundling."]
theorem mulHom_image_iff [DecidableEq H] (f : G ‚Üí‚Çô* H) (hf : Function.Injective f) :
    UniqueMul (A.image f) (B.image f) (f a0) (f b0) ‚Üî UniqueMul A B a0 b0 := by
  simp_rw [UniqueMul, Finset.mem_image]
  -- ‚ä¢ (‚àÄ ‚¶Éa b : H‚¶Ñ, (‚àÉ a_1, a_1 ‚àà A ‚àß ‚Üëf a_1 = a) ‚Üí (‚àÉ a, a ‚àà B ‚àß ‚Üëf a = b) ‚Üí a *  ‚Ä¶
  refine' ‚ü®fun h a b ha hb ab ‚Ü¶ _, fun h ‚Ü¶ _‚ü©
  -- ‚ä¢ a = a0 ‚àß b = b0
  ¬∑ rw [‚Üê hf.eq_iff, map_mul, map_mul] at ab
    -- ‚ä¢ a = a0 ‚àß b = b0
    have := h ‚ü®a, ha, rfl‚ü© ‚ü®b, hb, rfl‚ü© ab
    -- ‚ä¢ a = a0 ‚àß b = b0
    exact ‚ü®hf this.1, hf this.2‚ü©
    -- üéâ no goals
  ¬∑ rintro _ _ ‚ü®a, aA, rfl‚ü© ‚ü®b, bB, rfl‚ü© ab
    -- ‚ä¢ ‚Üëf a = ‚Üëf a0 ‚àß ‚Üëf b = ‚Üëf b0
    simp only [‚Üê map_mul, hf.eq_iff] at ab ‚ä¢
    -- ‚ä¢ a = a0 ‚àß b = b0
    exact h aA bB ab
    -- üéâ no goals
#align unique_mul.mul_hom_image_iff UniqueMul.mulHom_image_iff
#align unique_add.add_hom_image_iff UniqueAdd.addHom_image_iff

/-- `UniqueMul` is preserved under embeddings that are multiplicative.

See `UniqueMul.mulHom_image_iff` for a version with swapped bundling. -/
@[to_additive
      "`UniqueAdd` is preserved under embeddings that are additive.

See `UniqueAdd.addHom_image_iff` for a version with swapped bundling."]
theorem mulHom_map_iff (f : G ‚Ü™ H) (mul : ‚àÄ x y, f (x * y) = f x * f y) :
    UniqueMul (A.map f) (B.map f) (f a0) (f b0) ‚Üî UniqueMul A B a0 b0 := by
  classical
  convert @mulHom_image_iff G H _ _ A B a0 b0 _ ‚ü®f, mul‚ü© f.2 using 2 <;>
    ¬∑ ext
      simp only [Finset.mem_map, MulHom.coe_mk, Finset.mem_image]
#align unique_mul.mul_hom_map_iff UniqueMul.mulHom_map_iff
#align unique_add.add_hom_map_iff UniqueAdd.addHom_map_iff

end UniqueMul

/-- Let `G` be a Type with addition.  `UniqueSums G` asserts that any two non-empty
finite subsets of `A` have the `UniqueAdd` property, with respect to some element of their
sum `A + B`. -/
class UniqueSums (G) [Add G] : Prop where
/-- For `A B` two nonempty finite sets, there always exist `a0 ‚àà A, b0 ‚àà B` such that
`UniqueAdd A B a0 b0` -/
  uniqueAdd_of_nonempty :
    ‚àÄ {A B : Finset G} (_ : A.Nonempty) (_ : B.Nonempty), ‚àÉ a0 ‚àà A, ‚àÉ b0 ‚àà B, UniqueAdd A B a0 b0
#align unique_sums UniqueSums

/-- Let `G` be a Type with multiplication.  `UniqueProds G` asserts that any two non-empty
finite subsets of `G` have the `UniqueMul` property, with respect to some element of their
product `A * B`. -/
class UniqueProds (G) [Mul G] : Prop where
/-- For `A B` two nonempty finite sets, there always exist `a0 ‚àà A, b0 ‚àà B` such that
`UniqueMul A B a0 b0` -/
  uniqueMul_of_nonempty :
    ‚àÄ {A B : Finset G} (_ : A.Nonempty) (_ : B.Nonempty), ‚àÉ a0 ‚àà A, ‚àÉ b0 ‚àà B, UniqueMul A B a0 b0
#align unique_prods UniqueProds

attribute [to_additive] UniqueProds

namespace Multiplicative

instance {M} [Add M] [UniqueSums M] : UniqueProds (Multiplicative M) where
  uniqueMul_of_nonempty := UniqueSums.uniqueAdd_of_nonempty (G := M)

end Multiplicative

namespace Additive

instance {M} [Mul M] [UniqueProds M] : UniqueSums (Additive M) where
  uniqueAdd_of_nonempty := UniqueProds.uniqueMul_of_nonempty (G := M)

end Additive

-- see Note [lower instance priority]
/-- This instance asserts that if `A` has a multiplication, a linear order, and multiplication
is 'very monotone', then `A` also has `UniqueProds`. -/
@[to_additive
      "This instance asserts that if `A` has an addition, a linear order, and addition
is 'very monotone', then `A` also has `UniqueSums`."]
instance (priority := 100) Covariants.to_uniqueProds {A} [Mul A] [LinearOrder A]
    [CovariantClass A A (¬∑ * ¬∑) (¬∑ ‚â§ ¬∑)] [CovariantClass A A (Function.swap (¬∑ * ¬∑)) (¬∑ < ¬∑)]
    [ContravariantClass A A (¬∑ * ¬∑) (¬∑ ‚â§ ¬∑)] : UniqueProds A where
      uniqueMul_of_nonempty {A} {B} hA hB :=
        ‚ü®_, A.min'_mem ‚Äπ_‚Ä∫, _, B.min'_mem ‚Äπ_‚Ä∫, fun a b ha hb ab ‚Ü¶
        eq_and_eq_of_le_of_le_of_mul_le (Finset.min'_le _ _ ‚Äπ_‚Ä∫) (Finset.min'_le _ _ ‚Äπ_‚Ä∫) ab.le‚ü©
#align covariants.to_unique_prods Covariants.to_uniqueProds
#align covariants.to_unique_sums Covariants.to_uniqueSums
