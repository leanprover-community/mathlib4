/-
Copyright (c) 2021 Bryan Gin-ge Chen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bryan Gin-ge Chen, Yury Kudryashov
-/
import Mathlib.Algebra.Hom.Group

#align_import algebra.group.ext from "leanprover-community/mathlib"@"e574b1a4e891376b0ef974b926da39e05da12a06"

/-!
# Extensionality lemmas for monoid and group structures

In this file we prove extensionality lemmas for `Monoid` and higher algebraic structures with one
binary operation. Extensionality lemmas for structures that are lower in the hierarchy can be found
in `Algebra.Group.Defs`.

## Implementation details

To get equality of `npow` etc, we define a monoid homomorphism between two monoid structures on the
same type, then apply lemmas like `MonoidHom.map_div`, `MonoidHom.map_pow` etc.

## Tags
monoid, group, extensionality
-/

universe u

@[to_additive (attr := ext)]
theorem Monoid.ext {M : Type u} ‚¶Ém‚ÇÅ m‚ÇÇ : Monoid M‚¶Ñ (h_mul : m‚ÇÅ.mul = m‚ÇÇ.mul) : m‚ÇÅ = m‚ÇÇ := by
  have : m‚ÇÅ.toMulOneClass = m‚ÇÇ.toMulOneClass := MulOneClass.ext h_mul
  -- ‚ä¢ m‚ÇÅ = m‚ÇÇ
  have h‚ÇÅ : m‚ÇÅ.one = m‚ÇÇ.one := congr_arg (¬∑.one) (this)
  -- ‚ä¢ m‚ÇÅ = m‚ÇÇ
  let f : @MonoidHom M M m‚ÇÅ.toMulOneClass m‚ÇÇ.toMulOneClass :=
    @MonoidHom.mk _ _ (_) _ (@OneHom.mk _ _ (_) _ id h‚ÇÅ)
      (fun x y => congr_fun (congr_fun h_mul x) y)
  have : m‚ÇÅ.npow = m‚ÇÇ.npow := by
    ext n x
    exact @MonoidHom.map_pow M M m‚ÇÅ m‚ÇÇ f x n
  rcases m‚ÇÅ with @‚ü®@‚ü®‚ü®_‚ü©‚ü©, ‚ü®_‚ü©‚ü©
  -- ‚ä¢ mk one_mul‚úù mul_one‚úù npow‚úù = m‚ÇÇ
  rcases m‚ÇÇ with @‚ü®@‚ü®‚ü®_‚ü©‚ü©, ‚ü®_‚ü©‚ü©
  -- ‚ä¢ mk one_mul‚úù¬π mul_one‚úù¬π npow‚úù¬π = mk one_mul‚úù mul_one‚úù npow‚úù
  congr
  -- üéâ no goals
#align monoid.ext Monoid.ext
#align add_monoid.ext AddMonoid.ext

@[to_additive]
theorem CommMonoid.toMonoid_injective {M : Type u} :
    Function.Injective (@CommMonoid.toMonoid M) := by
  rintro ‚ü®‚ü© ‚ü®‚ü© h
  -- ‚ä¢ mk mul_comm‚úù¬π = mk mul_comm‚úù
  congr
  -- üéâ no goals
#align comm_monoid.to_monoid_injective CommMonoid.toMonoid_injective
#align add_comm_monoid.to_add_monoid_injective AddCommMonoid.toAddMonoid_injective

@[to_additive (attr := ext)]
theorem CommMonoid.ext {M : Type*} ‚¶Ém‚ÇÅ m‚ÇÇ : CommMonoid M‚¶Ñ (h_mul : m‚ÇÅ.mul = m‚ÇÇ.mul) : m‚ÇÅ = m‚ÇÇ :=
  CommMonoid.toMonoid_injective <| Monoid.ext h_mul
#align comm_monoid.ext CommMonoid.ext
#align add_comm_monoid.ext AddCommMonoid.ext

@[to_additive]
theorem LeftCancelMonoid.toMonoid_injective {M : Type u} :
    Function.Injective (@LeftCancelMonoid.toMonoid M) := by
  rintro @‚ü®@‚ü®‚ü©‚ü© @‚ü®@‚ü®‚ü©‚ü© h
  -- ‚ä¢ mk one_mul‚úù¬π mul_one‚úù¬π npow‚úù¬π = mk one_mul‚úù mul_one‚úù npow‚úù
  congr <;> injection h
            -- üéâ no goals
            -- üéâ no goals
            -- üéâ no goals
#align left_cancel_monoid.to_monoid_injective LeftCancelMonoid.toMonoid_injective
#align add_left_cancel_monoid.to_add_monoid_injective AddLeftCancelMonoid.toAddMonoid_injective

@[to_additive (attr := ext)]
theorem LeftCancelMonoid.ext {M : Type u} ‚¶Ém‚ÇÅ m‚ÇÇ : LeftCancelMonoid M‚¶Ñ (h_mul : m‚ÇÅ.mul = m‚ÇÇ.mul) :
    m‚ÇÅ = m‚ÇÇ :=
  LeftCancelMonoid.toMonoid_injective <| Monoid.ext h_mul
#align left_cancel_monoid.ext LeftCancelMonoid.ext
#align add_left_cancel_monoid.ext AddLeftCancelMonoid.ext

@[to_additive]
theorem RightCancelMonoid.toMonoid_injective {M : Type u} :
    Function.Injective (@RightCancelMonoid.toMonoid M) := by
  rintro @‚ü®@‚ü®‚ü©‚ü© @‚ü®@‚ü®‚ü©‚ü© h
  -- ‚ä¢ mk one_mul‚úù¬π mul_one‚úù¬π npow‚úù¬π = mk one_mul‚úù mul_one‚úù npow‚úù
  congr <;> injection h
            -- üéâ no goals
            -- üéâ no goals
            -- üéâ no goals
#align right_cancel_monoid.to_monoid_injective RightCancelMonoid.toMonoid_injective
#align add_right_cancel_monoid.to_add_monoid_injective AddRightCancelMonoid.toAddMonoid_injective

@[to_additive (attr := ext)]
theorem RightCancelMonoid.ext {M : Type u} ‚¶Ém‚ÇÅ m‚ÇÇ : RightCancelMonoid M‚¶Ñ (h_mul : m‚ÇÅ.mul = m‚ÇÇ.mul) :
    m‚ÇÅ = m‚ÇÇ :=
  RightCancelMonoid.toMonoid_injective <| Monoid.ext h_mul
#align right_cancel_monoid.ext RightCancelMonoid.ext
#align add_right_cancel_monoid.ext AddRightCancelMonoid.ext

@[to_additive]
theorem CancelMonoid.toLeftCancelMonoid_injective {M : Type u} :
    Function.Injective (@CancelMonoid.toLeftCancelMonoid M) := by
  rintro ‚ü®‚ü© ‚ü®‚ü© h
  -- ‚ä¢ mk mul_right_cancel‚úù¬π = mk mul_right_cancel‚úù
  congr
  -- üéâ no goals
#align cancel_monoid.to_left_cancel_monoid_injective CancelMonoid.toLeftCancelMonoid_injective
#align add_cancel_monoid.to_left_cancel_add_monoid_injective AddCancelMonoid.toAddLeftCancelMonoid_injective

@[to_additive (attr := ext)]
theorem CancelMonoid.ext {M : Type*} ‚¶Ém‚ÇÅ m‚ÇÇ : CancelMonoid M‚¶Ñ (h_mul : m‚ÇÅ.mul = m‚ÇÇ.mul) :
    m‚ÇÅ = m‚ÇÇ :=
  CancelMonoid.toLeftCancelMonoid_injective <| LeftCancelMonoid.ext h_mul
#align cancel_monoid.ext CancelMonoid.ext
#align add_cancel_monoid.ext AddCancelMonoid.ext

@[to_additive]
theorem CancelCommMonoid.toCommMonoid_injective {M : Type u} :
    Function.Injective (@CancelCommMonoid.toCommMonoid M) := by
  rintro @‚ü®@‚ü®@‚ü®‚ü©‚ü©‚ü© @‚ü®@‚ü®@‚ü®‚ü©‚ü©‚ü© h
  -- ‚ä¢ mk mul_comm‚úù¬π = mk mul_comm‚úù
  congr <;> {
    injection h with h'
    injection h' }
#align cancel_comm_monoid.to_comm_monoid_injective CancelCommMonoid.toCommMonoid_injective
#align add_cancel_comm_monoid.to_add_comm_monoid_injective AddCancelCommMonoid.toAddCommMonoid_injective

@[to_additive (attr := ext)]
theorem CancelCommMonoid.ext {M : Type*} ‚¶Ém‚ÇÅ m‚ÇÇ : CancelCommMonoid M‚¶Ñ (h_mul : m‚ÇÅ.mul = m‚ÇÇ.mul) :
    m‚ÇÅ = m‚ÇÇ :=
  CancelCommMonoid.toCommMonoid_injective <| CommMonoid.ext h_mul
#align cancel_comm_monoid.ext CancelCommMonoid.ext
#align add_cancel_comm_monoid.ext AddCancelCommMonoid.ext

@[to_additive (attr := ext)]
theorem DivInvMonoid.ext {M : Type*} ‚¶Ém‚ÇÅ m‚ÇÇ : DivInvMonoid M‚¶Ñ (h_mul : m‚ÇÅ.mul = m‚ÇÇ.mul)
  (h_inv : m‚ÇÅ.inv = m‚ÇÇ.inv) : m‚ÇÅ = m‚ÇÇ := by
  have h_mon := Monoid.ext h_mul
  -- ‚ä¢ m‚ÇÅ = m‚ÇÇ
  have h‚ÇÅ : m‚ÇÅ.one = m‚ÇÇ.one := congr_arg (¬∑.one) h_mon
  -- ‚ä¢ m‚ÇÅ = m‚ÇÇ
  let f : @MonoidHom M M m‚ÇÅ.toMulOneClass m‚ÇÇ.toMulOneClass :=
    @MonoidHom.mk _ _ (_) _ (@OneHom.mk _ _ (_) _ id h‚ÇÅ)
      (fun x y => congr_fun (congr_fun h_mul x) y)
  have : m‚ÇÅ.npow = m‚ÇÇ.npow := congr_arg (¬∑.npow) h_mon
  -- ‚ä¢ m‚ÇÅ = m‚ÇÇ
  have : m‚ÇÅ.zpow = m‚ÇÇ.zpow := by
    ext m x
    exact @MonoidHom.map_zpow' M M m‚ÇÅ m‚ÇÇ f (congr_fun h_inv) x m
  have : m‚ÇÅ.div = m‚ÇÇ.div := by
    ext a b
    exact @map_div' _ _
      (@MonoidHom _ _ (_) _) (id _) _
      (@MonoidHom.monoidHomClass _ _ (_) _) f (congr_fun h_inv) a b
  rcases m‚ÇÅ with @‚ü®_, ‚ü®_‚ü©, ‚ü®_‚ü©‚ü©
  -- ‚ä¢ mk zpow‚úù = m‚ÇÇ
  rcases m‚ÇÇ with @‚ü®_, ‚ü®_‚ü©, ‚ü®_‚ü©‚ü©
  -- ‚ä¢ mk zpow‚úù¬π = mk zpow‚úù
  congr
  -- üéâ no goals
#align div_inv_monoid.ext DivInvMonoid.ext
#align sub_neg_monoid.ext SubNegMonoid.ext

@[to_additive (attr := ext)]
theorem Group.ext {G : Type*} ‚¶Ég‚ÇÅ g‚ÇÇ : Group G‚¶Ñ (h_mul : g‚ÇÅ.mul = g‚ÇÇ.mul) : g‚ÇÅ = g‚ÇÇ := by
  have h‚ÇÅ : g‚ÇÅ.one = g‚ÇÇ.one := congr_arg (¬∑.one) (Monoid.ext h_mul)
  -- ‚ä¢ g‚ÇÅ = g‚ÇÇ
  let f : @MonoidHom G G g‚ÇÅ.toMulOneClass g‚ÇÇ.toMulOneClass :=
    @MonoidHom.mk _ _ (_) _ (@OneHom.mk _ _ (_) _ id h‚ÇÅ)
      (fun x y => congr_fun (congr_fun h_mul x) y)
  exact
    Group.toDivInvMonoid_injective
      (DivInvMonoid.ext h_mul
        (funext <| @MonoidHom.map_inv G G g‚ÇÅ g‚ÇÇ.toDivisionMonoid f))
#align group.ext Group.ext
#align add_group.ext AddGroup.ext

@[to_additive (attr := ext)]
theorem CommGroup.ext {G : Type*} ‚¶Ég‚ÇÅ g‚ÇÇ : CommGroup G‚¶Ñ (h_mul : g‚ÇÅ.mul = g‚ÇÇ.mul) : g‚ÇÅ = g‚ÇÇ :=
  CommGroup.toGroup_injective <| Group.ext h_mul
#align comm_group.ext CommGroup.ext
#align add_comm_group.ext AddCommGroup.ext
