/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Algebra.BigOperators.Multiset.Basic
import Mathlib.Data.FunLike.Basic

#align_import algebra.hom.freiman from "leanprover-community/mathlib"@"f694c7dead66f5d4c80f446c796a5aad14707f0e"

/-!
# Freiman homomorphisms

In this file, we define Freiman homomorphisms. An `n`-Freiman homomorphism on `A` is a function
`f : Î± â†’ Î²` such that `f (xâ‚) * ... * f (xâ‚™) = f (yâ‚) * ... * f (yâ‚™)` for all
`xâ‚, ..., xâ‚™, yâ‚, ..., yâ‚™ âˆˆ A` such that `xâ‚ * ... * xâ‚™ = yâ‚ * ... * yâ‚™`. In particular, any
`MulHom` is a Freiman homomorphism.

They are of interest in additive combinatorics.

## Main declaration

* `FreimanHom`: Freiman homomorphism.
* `AddFreimanHom`: Additive Freiman homomorphism.

## Notation

* `A â†’*[n] Î²`: Multiplicative `n`-Freiman homomorphism on `A`
* `A â†’+[n] Î²`: Additive `n`-Freiman homomorphism on `A`

## Implementation notes

In the context of combinatorics, we are interested in Freiman homomorphisms over sets which are not
necessarily closed under addition/multiplication. This means we must parametrize them with a set in
an `AddMonoid`/`Monoid` instead of the `AddMonoid`/`Monoid` itself.

## References

[Yufei Zhao, *18.225: Graph Theory and Additive Combinatorics*](https://yufeizhao.com/gtac/)

## TODO

`MonoidHom.toFreimanHom` could be relaxed to `MulHom.toFreimanHom` by proving
`(s.map f).prod = (t.map f).prod` directly by induction instead of going through `f s.prod`.

Define `n`-Freiman isomorphisms.

Affine maps induce Freiman homs. Concretely, provide the `AddFreimanHomClass (Î± â†’â‚[ð•œ] Î²) A Î² n`
instance.
-/


open Multiset

variable {F Î± Î² Î³ Î´ G : Type*}

/-- An additive `n`-Freiman homomorphism is a map which preserves sums of `n` elements. -/
structure AddFreimanHom (A : Set Î±) (Î² : Type*) [AddCommMonoid Î±] [AddCommMonoid Î²] (n : â„•) where
  /-- The underlying function. -/
  toFun : Î± â†’ Î²
  /-- An additive `n`-Freiman homomorphism preserves sums of `n` elements. -/
  map_sum_eq_map_sum' {s t : Multiset Î±} (hsA : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ x âˆˆ A) (htA : âˆ€ â¦ƒxâ¦„, x âˆˆ t â†’ x âˆˆ A)
    (hs : Multiset.card s = n) (ht : Multiset.card t = n) (h : s.sum = t.sum) :
    (s.map toFun).sum = (t.map toFun).sum
#align add_freiman_hom AddFreimanHom

/-- An `n`-Freiman homomorphism on a set `A` is a map which preserves products of `n` elements. -/
@[to_additive AddFreimanHom]
structure FreimanHom (A : Set Î±) (Î² : Type*) [CommMonoid Î±] [CommMonoid Î²] (n : â„•) where
  /-- The underlying function. -/
  toFun : Î± â†’ Î²
  /-- An `n`-Freiman homomorphism preserves products of `n` elements. -/
  map_prod_eq_map_prod' {s t : Multiset Î±} (hsA : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ x âˆˆ A) (htA : âˆ€ â¦ƒxâ¦„, x âˆˆ t â†’ x âˆˆ A)
    (hs : Multiset.card s = n) (ht : Multiset.card t = n) (h : s.prod = t.prod) :
    (s.map toFun).prod = (t.map toFun).prod
#align freiman_hom FreimanHom

/- Porting note: local notation given a name, conflict with Algebra.Hom.GroupAction
 see https://github.com/leanprover/lean4/issues/2000 -/
@[inherit_doc]
notation:25 (name := Â«AddFreimanHomLocalâ‰ºÂ») A " â†’+[" n:25 "] " Î²:0 => AddFreimanHom A Î² n

/- Porting note: local notation given a name, conflict with Algebra.Hom.GroupAction
 see https://github.com/leanprover/lean4/issues/2000 -/
@[inherit_doc]
notation:25 (name := Â«FreimanHomLocalâ‰ºÂ») A " â†’*[" n:25 "] " Î²:0 => FreimanHom A Î² n

/-- `AddFreimanHomClass F A Î² n` states that `F` is a type of `n`-ary sums-preserving morphisms.
You should extend this class when you extend `AddFreimanHom`. -/
class AddFreimanHomClass (F : Type*) (A : outParam <| Set Î±) (Î² : outParam <| Type*)
  [AddCommMonoid Î±] [AddCommMonoid Î²] (n : â„•) [FunLike F Î± Î²] : Prop where
  /-- An additive `n`-Freiman homomorphism preserves sums of `n` elements. -/
  map_sum_eq_map_sum' (f : F) {s t : Multiset Î±} (hsA : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ x âˆˆ A)
    (htA : âˆ€ â¦ƒxâ¦„, x âˆˆ t â†’ x âˆˆ A) (hs : Multiset.card s = n) (ht : Multiset.card t = n)
    (h : s.sum = t.sum) :
    (s.map f).sum = (t.map f).sum
#align add_freiman_hom_class AddFreimanHomClass

/-- `FreimanHomClass F A Î² n` states that `F` is a type of `n`-ary products-preserving morphisms.
You should extend this class when you extend `FreimanHom`. -/
@[to_additive AddFreimanHomClass
      "`AddFreimanHomClass F A Î² n` states that `F` is a type of `n`-ary
      sums-preserving morphisms. You should extend this class when you extend `AddFreimanHom`."]
class FreimanHomClass (F : Type*) (A : outParam <| Set Î±) (Î² : outParam <| Type*) [CommMonoid Î±]
  [CommMonoid Î²] (n : â„•) [FunLike F Î± Î²] : Prop where
  /-- An `n`-Freiman homomorphism preserves products of `n` elements. -/
  map_prod_eq_map_prod' (f : F) {s t : Multiset Î±} (hsA : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ x âˆˆ A)
    (htA : âˆ€ â¦ƒxâ¦„, x âˆˆ t â†’ x âˆˆ A) (hs : Multiset.card s = n) (ht : Multiset.card t = n)
    (h : s.prod = t.prod) :
    (s.map f).prod = (t.map f).prod
#align freiman_hom_class FreimanHomClass

variable [FunLike F Î± Î²]

section CommMonoid

variable [CommMonoid Î±] [CommMonoid Î²] [CommMonoid Î³] [CommMonoid Î´] [CommGroup G] {A : Set Î±}
  {B : Set Î²} {C : Set Î³} {n : â„•} {a b c d : Î±}

/- porting note: inserted following def & instance for consistent coercion behaviour,
see also Algebra.Hom.Group for similar -/
/-- Turn an element of a type `F` satisfying `FreimanHomClass F A Î² n` into an actual
`FreimanHom`. This is declared as the default coercion from `F` to `FreimanHom A Î² n`. -/
@[to_additive (attr := coe)
    " Turn an element of a type `F` satisfying `AddFreimanHomClass F A Î² n` into an actual
    `AddFreimanHom`. This is declared as the default coercion from `F` to `AddFreimanHom A Î² n`."]
def _root_.FreimanHomClass.toFreimanHom [FreimanHomClass F A Î² n] (f : F) : A â†’*[n] Î² where
  toFun := DFunLike.coe f
  map_prod_eq_map_prod' := FreimanHomClass.map_prod_eq_map_prod' f

/-- Any type satisfying `SMulHomClass` can be cast into `MulActionHom` via
  `SMulHomClass.toMulActionHom`. -/
instance [FreimanHomClass F A Î² n] : CoeTC F (A â†’*[n] Î²) :=
  âŸ¨FreimanHomClass.toFreimanHomâŸ©


@[to_additive]
theorem map_prod_eq_map_prod [FreimanHomClass F A Î² n] (f : F) {s t : Multiset Î±}
    (hsA : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ x âˆˆ A) (htA : âˆ€ â¦ƒxâ¦„, x âˆˆ t â†’ x âˆˆ A)
    (hs : Multiset.card s = n) (ht : Multiset.card t = n)
    (h : s.prod = t.prod) : (s.map f).prod = (t.map f).prod :=
  FreimanHomClass.map_prod_eq_map_prod' f hsA htA hs ht h
#align map_prod_eq_map_prod map_prod_eq_map_prod
#align map_sum_eq_map_sum map_sum_eq_map_sum

@[to_additive]
theorem map_mul_map_eq_map_mul_map [FreimanHomClass F A Î² 2] (f : F) (ha : a âˆˆ A) (hb : b âˆˆ A)
    (hc : c âˆˆ A) (hd : d âˆˆ A) (h : a * b = c * d) : f a * f b = f c * f d := by
  simp_rw [â† prod_pair] at h âŠ¢
  refine' map_prod_eq_map_prod f _ _ (card_pair _ _) (card_pair _ _) h <;> simp [ha, hb, hc, hd]
#align map_mul_map_eq_map_mul_map map_mul_map_eq_map_mul_map
#align map_add_map_eq_map_add_map map_add_map_eq_map_add_map

namespace FreimanHom

@[to_additive]
instance instFunLike : FunLike (A â†’*[n] Î²) Î± Î² where
  coe := toFun
  coe_injective' f g h := by cases f; cases g; congr
#align freiman_hom.fun_like FreimanHom.instFunLike
#align add_freiman_hom.fun_like AddFreimanHom.instFunLike

@[to_additive addFreimanHomClass]
instance freimanHomClass : FreimanHomClass (A â†’*[n] Î²) A Î² n where
  map_prod_eq_map_prod' := map_prod_eq_map_prod'
#align freiman_hom.freiman_hom_class FreimanHom.freimanHomClass
#align add_freiman_hom.freiman_hom_class AddFreimanHom.addFreimanHomClass

-- Porting note: not helpful in lean4
-- /-- Helper instance for when there's too many metavariables to apply `DFunLike.hasCoeToFun`
-- directly. -/
-- @[to_additive
--       "Helper instance for when there's too many metavariables to apply
--       `fun_like.has_coe_to_fun` directly."]
-- instance : CoeFun (A â†’*[n] Î²) fun _ => Î± â†’ Î² :=
--   âŸ¨toFunâŸ©

initialize_simps_projections FreimanHom (toFun â†’ apply)
initialize_simps_projections AddFreimanHom (toFun â†’ apply)

@[to_additive (attr := simp)]
theorem toFun_eq_coe (f : A â†’*[n] Î²) : f.toFun = f :=
  rfl
#align freiman_hom.to_fun_eq_coe FreimanHom.toFun_eq_coe
#align add_freiman_hom.to_fun_eq_coe AddFreimanHom.toFun_eq_coe

@[to_additive (attr := ext)]
theorem ext â¦ƒf g : A â†’*[n] Î²â¦„ (h : âˆ€ x, f x = g x) : f = g :=
  DFunLike.ext f g h
#align freiman_hom.ext FreimanHom.ext
#align add_freiman_hom.ext AddFreimanHom.ext

@[to_additive (attr := simp)]
theorem coe_mk (f : Î± â†’ Î²)
    (h :
      âˆ€ s t : Multiset Î±,
        (âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ x âˆˆ A) â†’
          (âˆ€ â¦ƒxâ¦„, x âˆˆ t â†’ x âˆˆ A) â†’
            Multiset.card s = n â†’ Multiset.card t = n â†’
              s.prod = t.prod â†’ (s.map f).prod = (t.map f).prod) :
    â‡‘(mk f (h _ _)) = f :=
  rfl
#align freiman_hom.coe_mk FreimanHom.coe_mk
#align add_freiman_hom.coe_mk AddFreimanHom.coe_mk

@[to_additive (attr := simp)]
theorem mk_coe (f : A â†’*[n] Î²) (h) : mk f h = f :=
  ext fun _ => rfl
#align freiman_hom.mk_coe FreimanHom.mk_coe
#align add_freiman_hom.mk_coe AddFreimanHom.mk_coe

/-- The identity map from a commutative monoid to itself. -/
@[to_additive (attr := simps) "The identity map from an additive commutative monoid to itself."]
protected def id (A : Set Î±) (n : â„•) : A â†’*[n] Î± where
  toFun x := x
  map_prod_eq_map_prod' _ _ _ _ h := by rw [map_id', map_id', h]
#align freiman_hom.id FreimanHom.id
#align add_freiman_hom.id AddFreimanHom.id
#align freiman_hom.id_apply FreimanHom.id_apply

/-- Composition of Freiman homomorphisms as a Freiman homomorphism. -/
@[to_additive "Composition of additive Freiman homomorphisms as an additive Freiman homomorphism."]
protected def comp (f : B â†’*[n] Î³) (g : A â†’*[n] Î²) (hAB : A.MapsTo g B) : A â†’*[n] Î³ where
  toFun := f âˆ˜ g
  map_prod_eq_map_prod' hsA htA hs ht h := by
    rw [â† map_map, â† map_map]
    apply map_prod_eq_map_prod f _ _ ((card_map _ _).trans hs)
    Â· rwa [card_map]
    Â· apply (map_prod_eq_map_prod g hsA htA hs ht h)
    Â· simpa using fun a h => hAB (hsA h)
    Â· simpa using fun a h => hAB (htA h)
#align freiman_hom.comp FreimanHom.comp
#align add_freiman_hom.comp AddFreimanHom.comp

@[to_additive (attr := simp)]
theorem coe_comp (f : B â†’*[n] Î³) (g : A â†’*[n] Î²) {hfg} : â‡‘(f.comp g hfg) = f âˆ˜ g :=
  rfl
#align freiman_hom.coe_comp FreimanHom.coe_comp
#align add_freiman_hom.coe_comp AddFreimanHom.coe_comp

@[to_additive]
theorem comp_apply (f : B â†’*[n] Î³) (g : A â†’*[n] Î²) {hfg} (x : Î±) : f.comp g hfg x = f (g x) :=
  rfl
#align freiman_hom.comp_apply FreimanHom.comp_apply
#align add_freiman_hom.comp_apply AddFreimanHom.comp_apply

@[to_additive]
theorem comp_assoc (f : A â†’*[n] Î²) (g : B â†’*[n] Î³) (h : C â†’*[n] Î´) {hf hhg hgf}
    {hh : A.MapsTo (g.comp f hgf) C} : (h.comp g hhg).comp f hf = h.comp (g.comp f hgf) hh :=
  rfl
#align freiman_hom.comp_assoc FreimanHom.comp_assoc
#align add_freiman_hom.comp_assoc AddFreimanHom.comp_assoc

@[to_additive (attr := simp)]
theorem cancel_right {gâ‚ gâ‚‚ : B â†’*[n] Î³} {f : A â†’*[n] Î²} (hf : Function.Surjective f) {hgâ‚ hgâ‚‚} :
    gâ‚.comp f hgâ‚ = gâ‚‚.comp f hgâ‚‚ â†” gâ‚ = gâ‚‚ :=
  âŸ¨fun h => ext <| hf.forall.2 <| DFunLike.ext_iff.1 h, fun h => h â–¸ rflâŸ©
#align freiman_hom.cancel_right FreimanHom.cancel_right
#align add_freiman_hom.cancel_right AddFreimanHom.cancel_right

@[to_additive]
theorem cancel_right_on {gâ‚ gâ‚‚ : B â†’*[n] Î³} {f : A â†’*[n] Î²} (hf : A.SurjOn f B) {hf'} :
    A.EqOn (gâ‚.comp f hf') (gâ‚‚.comp f hf') â†” B.EqOn gâ‚ gâ‚‚ :=
  by simp [hf.cancel_right hf']
#align freiman_hom.cancel_right_on FreimanHom.cancel_right_on
#align add_freiman_hom.cancel_right_on AddFreimanHom.cancel_right_on

@[to_additive]
theorem cancel_left_on {g : B â†’*[n] Î³} {fâ‚ fâ‚‚ : A â†’*[n] Î²} (hg : B.InjOn g) {hfâ‚ hfâ‚‚} :
    A.EqOn (g.comp fâ‚ hfâ‚) (g.comp fâ‚‚ hfâ‚‚) â†” A.EqOn fâ‚ fâ‚‚ :=
  by simp [hg.cancel_left hfâ‚ hfâ‚‚]
#align freiman_hom.cancel_left_on FreimanHom.cancel_left_on
#align add_freiman_hom.cancel_left_on AddFreimanHom.cancel_left_on

@[to_additive (attr := simp)]
theorem comp_id (f : A â†’*[n] Î²) {hf} : f.comp (FreimanHom.id A n) hf = f :=
  ext fun _ => rfl
#align freiman_hom.comp_id FreimanHom.comp_id
#align add_freiman_hom.comp_id AddFreimanHom.comp_id

@[to_additive (attr := simp)]
theorem id_comp (f : A â†’*[n] Î²) {hf} : (FreimanHom.id B n).comp f hf = f :=
  ext fun _ => rfl
#align freiman_hom.id_comp FreimanHom.id_comp
#align add_freiman_hom.id_comp AddFreimanHom.id_comp

/-- `FreimanHom.const A n b` is the Freiman homomorphism sending everything to `b`. -/
@[to_additive "`AddFreimanHom.const An b` is the Freiman homomorphism sending everything to `b`."]
def const (A : Set Î±) (n : â„•) (b : Î²) : A â†’*[n] Î² where
  toFun _ := b
  map_prod_eq_map_prod' _ _ hs ht _ := by
    simp only [map_const', hs, prod_replicate, ht]
#align freiman_hom.const FreimanHom.const
#align add_freiman_hom.const AddFreimanHom.const

@[to_additive (attr := simp)]
theorem const_apply (n : â„•) (b : Î²) (x : Î±) : const A n b x = b :=
  rfl
#align freiman_hom.const_apply FreimanHom.const_apply
#align add_freiman_hom.const_apply AddFreimanHom.const_apply

@[to_additive (attr := simp)]
theorem const_comp (n : â„•) (c : Î³) (f : A â†’*[n] Î²) {hf} : (const B n c).comp f hf = const A n c :=
  rfl
#align freiman_hom.const_comp FreimanHom.const_comp
#align add_freiman_hom.const_comp AddFreimanHom.const_comp

/-- `1` is the Freiman homomorphism sending everything to `1`. -/
@[to_additive "`0` is the Freiman homomorphism sending everything to `0`."]
instance : One (A â†’*[n] Î²) :=
  âŸ¨const A n 1âŸ©

@[to_additive (attr := simp)]
theorem one_apply (x : Î±) : (1 : A â†’*[n] Î²) x = 1 :=
  rfl
#align freiman_hom.one_apply FreimanHom.one_apply
#align add_freiman_hom.zero_apply AddFreimanHom.zero_apply

@[to_additive (attr := simp)]
theorem one_comp (f : A â†’*[n] Î²) {hf} : (1 : B â†’*[n] Î³).comp f hf = 1 :=
  rfl
#align freiman_hom.one_comp FreimanHom.one_comp
#align add_freiman_hom.zero_comp AddFreimanHom.zero_comp

@[to_additive]
instance : Inhabited (A â†’*[n] Î²) :=
  âŸ¨1âŸ©

/-- `f * g` is the Freiman homomorphism sends `x` to `f x * g x`. -/
@[to_additive "`f + g` is the Freiman homomorphism sending `x` to `f x + g x`."]
instance : Mul (A â†’*[n] Î²) :=
  âŸ¨fun f g =>
    { toFun := fun x => f x * g x
      map_prod_eq_map_prod' := fun hsA htA hs ht h => by
          rw [prod_map_mul, prod_map_mul]
          rw [map_prod_eq_map_prod f hsA htA hs ht h]
          rw [map_prod_eq_map_prod g hsA htA hs ht h]}âŸ©

@[to_additive (attr := simp)]
theorem mul_apply (f g : A â†’*[n] Î²) (x : Î±) : (f * g) x = f x * g x :=
  rfl
#align freiman_hom.mul_apply FreimanHom.mul_apply
#align add_freiman_hom.add_apply AddFreimanHom.add_apply

@[to_additive]
theorem mul_comp (gâ‚ gâ‚‚ : B â†’*[n] Î³) (f : A â†’*[n] Î²) {hg hgâ‚ hgâ‚‚} :
    (gâ‚ * gâ‚‚).comp f hg = gâ‚.comp f hgâ‚ * gâ‚‚.comp f hgâ‚‚ :=
  rfl
#align freiman_hom.mul_comp FreimanHom.mul_comp
#align add_freiman_hom.add_comp AddFreimanHom.add_comp

/-- If `f` is a Freiman homomorphism to a commutative group, then `fâ»Â¹` is the Freiman homomorphism
sending `x` to `(f x)â»Â¹`. -/
@[to_additive
      "If `f` is a Freiman homomorphism to an additive commutative group, then `-f` is the
      Freiman homomorphism sending `x` to `-f x`."]
instance : Inv (A â†’*[n] G) :=
  âŸ¨fun f =>
    { toFun := fun x => (f x)â»Â¹
      map_prod_eq_map_prod' := fun hsA htA hs ht h => by
        rw [prod_map_inv, prod_map_inv, map_prod_eq_map_prod f hsA htA hs ht h] }âŸ©

@[to_additive (attr := simp)]
theorem inv_apply (f : A â†’*[n] G) (x : Î±) : fâ»Â¹ x = (f x)â»Â¹ :=
  rfl
#align freiman_hom.inv_apply FreimanHom.inv_apply
#align add_freiman_hom.neg_apply AddFreimanHom.neg_apply

@[to_additive (attr := simp)]
theorem inv_comp (f : B â†’*[n] G) (g : A â†’*[n] Î²) {hf hf'} : fâ»Â¹.comp g hf = (f.comp g hf')â»Â¹ :=
  ext fun _ => rfl
#align freiman_hom.inv_comp FreimanHom.inv_comp
#align add_freiman_hom.neg_comp AddFreimanHom.neg_comp

/-- If `f` and `g` are Freiman homomorphisms to a commutative group, then `f / g` is the Freiman
homomorphism sending `x` to `f x / g x`. -/
@[to_additive
      "If `f` and `g` are additive Freiman homomorphisms to an additive commutative group,
      then `f - g` is the additive Freiman homomorphism sending `x` to `f x - g x`"]
instance : Div (A â†’*[n] G) :=
  âŸ¨fun f g =>
    { toFun := fun x => f x / g x
      map_prod_eq_map_prod' := fun hsA htA hs ht h => by
        rw [prod_map_div, prod_map_div, map_prod_eq_map_prod f hsA htA hs ht h,
          map_prod_eq_map_prod g hsA htA hs ht h] }âŸ©

@[to_additive (attr := simp)]
theorem div_apply (f g : A â†’*[n] G) (x : Î±) : (f / g) x = f x / g x :=
  rfl
#align freiman_hom.div_apply FreimanHom.div_apply
#align add_freiman_hom.sub_apply AddFreimanHom.sub_apply

@[to_additive (attr := simp)]
theorem div_comp (fâ‚ fâ‚‚ : B â†’*[n] G) (g : A â†’*[n] Î²) {hf hfâ‚ hfâ‚‚} :
    (fâ‚ / fâ‚‚).comp g hf = fâ‚.comp g hfâ‚ / fâ‚‚.comp g hfâ‚‚ :=
  ext fun _ => rfl
#align freiman_hom.div_comp FreimanHom.div_comp
#align add_freiman_hom.sub_comp AddFreimanHom.sub_comp

/-! ### Instances -/


/-- `A â†’*[n] Î²` is a `CommMonoid`. -/
@[to_additive "`Î± â†’+[n] Î²` is an `AddCommMonoid`."]
instance commMonoid : CommMonoid (A â†’*[n] Î²) where
  mul_assoc a b c := by
    ext
    apply mul_assoc
  one_mul a := by
    ext
    apply one_mul
  mul_one a := by
    ext
    apply mul_one
  mul_comm a b := by
    ext
    apply mul_comm
#align freiman_hom.comm_monoid FreimanHom.commMonoid
#align add_freiman_hom.add_comm_monoid AddFreimanHom.addCommMonoid

/-- If `Î²` is a commutative group, then `A â†’*[n] Î²` is a commutative group too. -/
@[to_additive
      "If `Î²` is an additive commutative group, then `A â†’*[n] Î²` is an additive commutative
      group too."]
instance commGroup {Î²} [CommGroup Î²] : CommGroup (A â†’*[n] Î²) :=
  { FreimanHom.commMonoid with
    div_eq_mul_inv := by
      intros
      ext
      apply div_eq_mul_inv
    mul_left_inv := by
      intros
      ext
      apply mul_left_inv}
#align freiman_hom.comm_group FreimanHom.commGroup
#align add_freiman_hom.add_comm_group AddFreimanHom.addCommGroup

end FreimanHom

/-! ### Hom hierarchy -/


--TODO: change to `MonoidHomClass F A Î² â†’ FreimanHomClass F A Î² n` once `map_multiset_prod` is
-- generalized
/-- A monoid homomorphism is naturally a `FreimanHom` on its entire domain.

We can't leave the domain `A : Set Î±` of the `FreimanHom` a free variable, since it wouldn't be
inferrable. -/
@[to_additive AddMonoidHom.addFreimanHomClass
      " An additive monoid homomorphism is naturally an `AddFreimanHom` on its entire
      domain.

      We can't leave the domain `A : Set Î±` of the `AddFreimanHom` a free variable, since it
      wouldn't be inferrable."]
instance MonoidHom.freimanHomClass : FreimanHomClass (Î± â†’* Î²) Set.univ Î² n where
  map_prod_eq_map_prod' f s t _ _ _ _ h := by
    rw [â† f.map_multiset_prod, h, f.map_multiset_prod]
#align monoid_hom.freiman_hom_class MonoidHom.freimanHomClass
#align add_monoid_hom.freiman_hom_class AddMonoidHom.addFreimanHomClass

/-- A `MonoidHom` is naturally a `FreimanHom`. -/
@[to_additive AddMonoidHom.toAddFreimanHom
      "An `AddMonoidHom` is naturally an `AddFreimanHom`"]
def MonoidHom.toFreimanHom (A : Set Î±) (n : â„•) (f : Î± â†’* Î²) : A â†’*[n] Î² where
  toFun := f
  map_prod_eq_map_prod' _ _ :=
    map_prod_eq_map_prod f (fun _ _ => Set.mem_univ _) fun _ _ => Set.mem_univ _
#align monoid_hom.to_freiman_hom MonoidHom.toFreimanHom
#align add_monoid_hom.to_add_freiman_hom AddMonoidHom.toAddFreimanHom

@[to_additive (attr := simp) toAddFreimanHom_coe]
theorem MonoidHom.toFreimanHom_coe (f : Î± â†’* Î²) : (f.toFreimanHom A n : Î± â†’ Î²) = f :=
  rfl
#align monoid_hom.to_freiman_hom_coe MonoidHom.toFreimanHom_coe
#align add_monoid_hom.to_freiman_hom_coe AddMonoidHom.toAddFreimanHom_coe

@[to_additive AddMonoidHom.toAddFreimanHom_injective]
theorem MonoidHom.toFreimanHom_injective :
    Function.Injective (MonoidHom.toFreimanHom A n : (Î± â†’* Î²) â†’ A â†’*[n] Î²) := fun f g h =>
   by rwa [toFreimanHom, toFreimanHom, FreimanHom.mk.injEq, DFunLike.coe_fn_eq] at h
#align monoid_hom.to_freiman_hom_injective MonoidHom.toFreimanHom_injective
#align add_monoid_hom.to_freiman_hom_injective AddMonoidHom.toAddFreimanHom_injective

end CommMonoid

section CancelCommMonoid

variable [CommMonoid Î±] [CancelCommMonoid Î²] {A : Set Î±} {m n : â„•}

@[to_additive]
theorem map_prod_eq_map_prod_of_le [FreimanHomClass F A Î² n] (f : F) {s t : Multiset Î±}
    (hsA : âˆ€ x âˆˆ s, x âˆˆ A) (htA : âˆ€ x âˆˆ t, x âˆˆ A)
    (hs : Multiset.card s = m) (ht : Multiset.card t = m)
    (hst : s.prod = t.prod) (h : m â‰¤ n) : (s.map f).prod = (t.map f).prod := by
  obtain rfl | hm := m.eq_zero_or_pos
  Â· rw [card_eq_zero] at hs ht
    rw [hs, ht]
  simp? [â† hs, card_pos_iff_exists_mem] at hm says
    simp only [â† hs, gt_iff_lt, card_pos_iff_exists_mem] at hm
  obtain âŸ¨a, haâŸ© := hm
  suffices
    ((s + Multiset.replicate (n - m) a).map f).prod =
      ((t + Multiset.replicate (n - m) a).map f).prod by
    simp_rw [Multiset.map_add, prod_add] at this
    exact mul_right_cancel this
  replace ha := hsA _ ha
  apply map_prod_eq_map_prod f (A := A) (Î² := Î²) (n := n) (fun x hx => _) (fun x hx => _) _ _ _
  -- Porting note: below could be golfed when wlog is available
  Â· intro x hx
    rw [mem_add] at hx
    cases' hx with hx hx
    Â· exact hsA x hx
    Â· rwa [eq_of_mem_replicate hx]
  Â· intro x hx
    rw [mem_add] at hx
    cases' hx with hx hx
    Â· exact htA x hx
    Â· rwa [eq_of_mem_replicate hx]
  Â· rw [_root_.map_add, card_replicate, hs]; simp [h]
  Â· rw [_root_.map_add, card_replicate, ht]; simp [h]
  Â· rw [prod_add, prod_add, hst]
#align map_prod_eq_map_prod_of_le map_prod_eq_map_prod_of_le
#align map_sum_eq_map_sum_of_le map_sum_eq_map_sum_of_le

/-- `Î± â†’*[n] Î²` is naturally included in `A â†’*[m] Î²` for any `m â‰¤ n`. -/
@[to_additive AddFreimanHom.toAddFreimanHom
      "`Î± â†’+[n] Î²` is naturally included in `Î± â†’+[m] Î²`
      for any `m â‰¤ n`"]
def FreimanHom.toFreimanHom (h : m â‰¤ n) (f : A â†’*[n] Î²) : A â†’*[m] Î² where
  toFun := f
  map_prod_eq_map_prod' hsA htA hs ht hst := map_prod_eq_map_prod_of_le f hsA htA hs ht hst h
#align freiman_hom.to_freiman_hom FreimanHom.toFreimanHom
#align add_freiman_hom.to_add_freiman_hom AddFreimanHom.toAddFreimanHom

/-- An `n`-Freiman homomorphism is also a `m`-Freiman homomorphism for any `m â‰¤ n`. -/
@[to_additive AddFreimanHom.addFreimanHomClass_of_le
      "An additive `n`-Freiman homomorphism is
      also an additive `m`-Freiman homomorphism for any `m â‰¤ n`."]
theorem FreimanHom.FreimanHomClass_of_le [FreimanHomClass F A Î² n] (h : m â‰¤ n) :
    FreimanHomClass F A Î² m where
  map_prod_eq_map_prod' f _ _ hsA htA hs ht hst :=
    map_prod_eq_map_prod_of_le f hsA htA hs ht hst h
#align freiman_hom.freiman_hom_class_of_le FreimanHom.FreimanHomClass_of_le
#align add_freiman_hom.add_freiman_hom_class_of_le AddFreimanHom.addFreimanHomClass_of_le

@[to_additive (attr := simp) AddFreimanHom.toAddFreimanHom_coe]
theorem FreimanHom.toFreimanHom_coe (h : m â‰¤ n) (f : A â†’*[n] Î²) :
    (f.toFreimanHom h : Î± â†’ Î²) = f :=
  rfl
#align freiman_hom.to_freiman_hom_coe FreimanHom.toFreimanHom_coe
#align add_freiman_hom.to_add_freiman_hom_coe AddFreimanHom.toAddFreimanHom_coe

@[to_additive AddFreimanHom.toAddFreimanHom_injective]
theorem FreimanHom.toFreimanHom_injective (h : m â‰¤ n) :
    Function.Injective (FreimanHom.toFreimanHom h : (A â†’*[n] Î²) â†’ A â†’*[m] Î²) := fun f g hfg =>
  FreimanHom.ext <| by convert DFunLike.ext_iff.1 hfg using 0
#align freiman_hom.to_freiman_hom_injective FreimanHom.toFreimanHom_injective
#align add_freiman_hom.to_freiman_hom_injective AddFreimanHom.toAddFreimanHom_injective

end CancelCommMonoid
