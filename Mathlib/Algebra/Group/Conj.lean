/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Chris Hughes, Michael Howes
-/
import Mathlib.Algebra.Group.Semiconj
import Mathlib.Algebra.GroupWithZero.Basic
import Mathlib.Algebra.Hom.Aut
import Mathlib.Algebra.Hom.Group

#align_import algebra.group.conj from "leanprover-community/mathlib"@"0743cc5d9d86bcd1bba10f480e948a257d65056f"

/-!
# Conjugacy of group elements

See also `MulAut.conj` and `Quandle.conj`.
-/

universe u v

variable {Î± : Type u} {Î² : Type v}

section Monoid

variable [Monoid Î±] [Monoid Î²]

/-- We say that `a` is conjugate to `b` if for some unit `c` we have `c * a * câ»Â¹ = b`. -/
def IsConj (a b : Î±) :=
  âˆƒ c : Î±Ë£, SemiconjBy (â†‘c) a b
#align is_conj IsConj

@[refl]
theorem IsConj.refl (a : Î±) : IsConj a a :=
  âŸ¨1, SemiconjBy.one_left aâŸ©
#align is_conj.refl IsConj.refl

@[symm]
theorem IsConj.symm {a b : Î±} : IsConj a b â†’ IsConj b a
  | âŸ¨c, hcâŸ© => âŸ¨câ»Â¹, hc.units_inv_symm_leftâŸ©
#align is_conj.symm IsConj.symm

theorem isConj_comm {g h : Î±} : IsConj g h â†” IsConj h g :=
  âŸ¨IsConj.symm, IsConj.symmâŸ©
#align is_conj_comm isConj_comm

@[trans]
theorem IsConj.trans {a b c : Î±} : IsConj a b â†’ IsConj b c â†’ IsConj a c
  | âŸ¨câ‚, hcâ‚âŸ©, âŸ¨câ‚‚, hcâ‚‚âŸ© => âŸ¨câ‚‚ * câ‚, hcâ‚‚.mul_left hcâ‚âŸ©
#align is_conj.trans IsConj.trans

@[simp]
theorem isConj_iff_eq {Î± : Type*} [CommMonoid Î±] {a b : Î±} : IsConj a b â†” a = b :=
  âŸ¨fun âŸ¨c, hcâŸ© => by
    rw [SemiconjBy, mul_comm, â† Units.mul_inv_eq_iff_eq_mul, mul_assoc, c.mul_inv, mul_one] at hc
    -- âŠ¢ a = b
    exact hc, fun h => by rw [h]âŸ©
    -- ðŸŽ‰ no goals
                          -- ðŸŽ‰ no goals
#align is_conj_iff_eq isConj_iff_eq

protected theorem MonoidHom.map_isConj (f : Î± â†’* Î²) {a b : Î±} : IsConj a b â†’ IsConj (f a) (f b)
  | âŸ¨c, hcâŸ© => âŸ¨Units.map f c, by rw [Units.coe_map, SemiconjBy, â† f.map_mul, hc.eq, f.map_mul]âŸ©
                                  -- ðŸŽ‰ no goals
#align monoid_hom.map_is_conj MonoidHom.map_isConj

end Monoid

section CancelMonoid

variable [CancelMonoid Î±]

-- These lemmas hold for `RightCancelMonoid` with the current proofs, but for the sake of
-- not duplicating code (these lemmas also hold for `LeftCancelMonoids`) we leave these
-- not generalised.
@[simp]
theorem isConj_one_right {a : Î±} : IsConj 1 a â†” a = 1 :=
  âŸ¨fun âŸ¨c, hcâŸ© => mul_right_cancel (hc.symm.trans ((mul_one _).trans (one_mul _).symm)), fun h => by
    rw [h]âŸ©
    -- ðŸŽ‰ no goals
#align is_conj_one_right isConj_one_right

@[simp]
theorem isConj_one_left {a : Î±} : IsConj a 1 â†” a = 1 :=
  calc
    IsConj a 1 â†” IsConj 1 a := âŸ¨IsConj.symm, IsConj.symmâŸ©
    _ â†” a = 1 := isConj_one_right
#align is_conj_one_left isConj_one_left

end CancelMonoid

section Group

variable [Group Î±]

@[simp]
theorem isConj_iff {a b : Î±} : IsConj a b â†” âˆƒ c : Î±, c * a * câ»Â¹ = b :=
  âŸ¨fun âŸ¨c, hcâŸ© => âŸ¨c, mul_inv_eq_iff_eq_mul.2 hcâŸ©, fun âŸ¨c, hcâŸ© =>
    âŸ¨âŸ¨c, câ»Â¹, mul_inv_self c, inv_mul_self câŸ©, mul_inv_eq_iff_eq_mul.1 hcâŸ©âŸ©
#align is_conj_iff isConj_iff

-- Porting note: not in simp NF.
-- @[simp]
theorem conj_inv {a b : Î±} : (b * a * bâ»Â¹)â»Â¹ = b * aâ»Â¹ * bâ»Â¹ :=
  ((MulAut.conj b).map_inv a).symm
#align conj_inv conj_inv

@[simp]
theorem conj_mul {a b c : Î±} : b * a * bâ»Â¹ * (b * c * bâ»Â¹) = b * (a * c) * bâ»Â¹ :=
  ((MulAut.conj b).map_mul a c).symm
#align conj_mul conj_mul

@[simp]
theorem conj_pow {i : â„•} {a b : Î±} : (a * b * aâ»Â¹) ^ i = a * b ^ i * aâ»Â¹ := by
  induction' i with i hi
  -- âŠ¢ (a * b * aâ»Â¹) ^ Nat.zero = a * b ^ Nat.zero * aâ»Â¹
  Â· simp
    -- ðŸŽ‰ no goals
  Â· simp [pow_succ, hi]
    -- ðŸŽ‰ no goals
#align conj_pow conj_pow

@[simp]
theorem conj_zpow {i : â„¤} {a b : Î±} : (a * b * aâ»Â¹) ^ i = a * b ^ i * aâ»Â¹ := by
  induction' i
  -- âŠ¢ (a * b * aâ»Â¹) ^ Int.ofNat aâœ = a * b ^ Int.ofNat aâœ * aâ»Â¹
  Â· change (a * b * aâ»Â¹) ^ (_ : â„¤) = a * b ^ (_ : â„¤) * aâ»Â¹
    -- âŠ¢ (a * b * aâ»Â¹) ^ Int.ofNat aâœ = a * b ^ Int.ofNat aâœ * aâ»Â¹
    simp [zpow_ofNat]
    -- ðŸŽ‰ no goals
  Â· simp [zpow_negSucc, conj_pow]
    -- âŠ¢ a * ((b ^ (aâœ + 1))â»Â¹ * aâ»Â¹) = a * (b ^ (aâœ + 1))â»Â¹ * aâ»Â¹
    rw [mul_assoc]
    -- ðŸŽ‰ no goals
-- Porting note: Added `change`, `zpow_ofNat`, and `rw`.
#align conj_zpow conj_zpow

theorem conj_injective {x : Î±} : Function.Injective fun g : Î± => x * g * xâ»Â¹ :=
  (MulAut.conj x).injective
#align conj_injective conj_injective

end Group

@[simp]
theorem isConj_iffâ‚€ [GroupWithZero Î±] {a b : Î±} : IsConj a b â†” âˆƒ c : Î±, c â‰  0 âˆ§ c * a * câ»Â¹ = b :=
  âŸ¨fun âŸ¨c, hcâŸ© =>
    âŸ¨c, by
      rw [â† Units.val_inv_eq_inv_val, Units.mul_inv_eq_iff_eq_mul]
      -- âŠ¢ â†‘c â‰  0 âˆ§ â†‘c * a = b * â†‘c
      exact âŸ¨c.ne_zero, hcâŸ©âŸ©,
      -- ðŸŽ‰ no goals
    fun âŸ¨c, c0, hcâŸ© =>
    âŸ¨Units.mk0 c c0, by
      rw [SemiconjBy, â† Units.mul_inv_eq_iff_eq_mul, Units.val_inv_eq_inv_val, Units.val_mk0]
      -- âŠ¢ c * a * câ»Â¹ = b
      exact hcâŸ©âŸ©
      -- ðŸŽ‰ no goals
#align is_conj_iffâ‚€ isConj_iffâ‚€

namespace IsConj

/- This small quotient API is largely copied from the API of `Associates`;
where possible, try to keep them in sync -/
/-- The setoid of the relation `IsConj` iff there is a unit `u` such that `u * x = y * u` -/
protected def setoid (Î± : Type*) [Monoid Î±] : Setoid Î± where
  r := IsConj
  iseqv := âŸ¨IsConj.refl, IsConj.symm, IsConj.transâŸ©
#align is_conj.setoid IsConj.setoid

end IsConj

attribute [local instance] IsConj.setoid

/-- The quotient type of conjugacy classes of a group. -/
def ConjClasses (Î± : Type*) [Monoid Î±] : Type _ :=
  Quotient (IsConj.setoid Î±)
#align conj_classes ConjClasses

namespace ConjClasses

section Monoid

variable [Monoid Î±] [Monoid Î²]

/-- The canonical quotient map from a monoid `Î±` into the `ConjClasses` of `Î±` -/
protected def mk {Î± : Type*} [Monoid Î±] (a : Î±) : ConjClasses Î± := âŸ¦aâŸ§
#align conj_classes.mk ConjClasses.mk

instance : Inhabited (ConjClasses Î±) := âŸ¨âŸ¦1âŸ§âŸ©

theorem mk_eq_mk_iff_isConj {a b : Î±} : ConjClasses.mk a = ConjClasses.mk b â†” IsConj a b :=
  Iff.intro Quotient.exact Quot.sound
#align conj_classes.mk_eq_mk_iff_is_conj ConjClasses.mk_eq_mk_iff_isConj

theorem quotient_mk_eq_mk (a : Î±) : âŸ¦aâŸ§ = ConjClasses.mk a :=
  rfl
#align conj_classes.quotient_mk_eq_mk ConjClasses.quotient_mk_eq_mk

theorem quot_mk_eq_mk (a : Î±) : Quot.mk Setoid.r a = ConjClasses.mk a :=
  rfl
#align conj_classes.quot_mk_eq_mk ConjClasses.quot_mk_eq_mk

theorem forall_isConj {p : ConjClasses Î± â†’ Prop} : (âˆ€ a, p a) â†” âˆ€ a, p (ConjClasses.mk a) :=
  Iff.intro (fun h _ => h _) fun h a => Quotient.inductionOn a h
#align conj_classes.forall_is_conj ConjClasses.forall_isConj

theorem mk_surjective : Function.Surjective (@ConjClasses.mk Î± _) :=
  forall_isConj.2 fun a => âŸ¨a, rflâŸ©
#align conj_classes.mk_surjective ConjClasses.mk_surjective

instance : One (ConjClasses Î±) :=
  âŸ¨âŸ¦1âŸ§âŸ©

theorem one_eq_mk_one : (1 : ConjClasses Î±) = ConjClasses.mk 1 :=
  rfl
#align conj_classes.one_eq_mk_one ConjClasses.one_eq_mk_one

theorem exists_rep (a : ConjClasses Î±) : âˆƒ a0 : Î±, ConjClasses.mk a0 = a :=
  Quot.exists_rep a
#align conj_classes.exists_rep ConjClasses.exists_rep

/-- A `MonoidHom` maps conjugacy classes of one group to conjugacy classes of another. -/
def map (f : Î± â†’* Î²) : ConjClasses Î± â†’ ConjClasses Î² :=
  Quotient.lift (ConjClasses.mk âˆ˜ f) fun _ _ ab => mk_eq_mk_iff_isConj.2 (f.map_isConj ab)
#align conj_classes.map ConjClasses.map

theorem map_surjective {f : Î± â†’* Î²} (hf : Function.Surjective f) :
    Function.Surjective (ConjClasses.map f) := by
  intro b
  -- âŠ¢ âˆƒ a, map f a = b
  obtain âŸ¨b, rflâŸ© := ConjClasses.mk_surjective b
  -- âŠ¢ âˆƒ a, map f a = ConjClasses.mk b
  obtain âŸ¨a, rflâŸ© := hf b
  -- âŠ¢ âˆƒ a_1, map f a_1 = ConjClasses.mk (â†‘f a)
  exact âŸ¨ConjClasses.mk a, rflâŸ©
  -- ðŸŽ‰ no goals
#align conj_classes.map_surjective ConjClasses.map_surjective

-- Porting note: This has not been adapted to mathlib4, is it still accurate?
library_note "slow-failing instance priority"/--
Certain instances trigger further searches when they are considered as candidate instances;
these instances should be assigned a priority lower than the default of 1000 (for example, 900).

The conditions for this rule are as follows:
 * a class `C` has instances `instT : C T` and `instT' : C T'`
 * types `T` and `T'` are both specializations of another type `S`
 * the parameters supplied to `S` to produce `T` are not (fully) determined by `instT`,
   instead they have to be found by instance search
If those conditions hold, the instance `instT` should be assigned lower priority.

For example, suppose the search for an instance of `DecidableEq (Multiset Î±)` tries the
candidate instance `Con.quotient.decidableEq (c : Con M) : decidableEq c.quotient`.
Since `Multiset` and `Con.quotient` are both quotient types, unification will check
that the relations `List.perm` and `c.toSetoid.r` unify. However, `c.toSetoid` depends on
a `Mul M` instance, so this unification triggers a search for `Mul (List Î±)`;
this will traverse all subclasses of `Mul` before failing.
On the other hand, the search for an instance of `DecidableEq (Con.quotient c)` for `c : Con M`
can quickly reject the candidate instance `Multiset.decidableEq` because the type of
`List.perm : List ?m_1 â†’ List ?m_1 â†’ Prop` does not unify with `M â†’ M â†’ Prop`.
Therefore, we should assign `Con.quotient.decidableEq` a lower priority because it fails slowly.
(In terms of the rules above, `C := DecidableEq`, `T := Con.quotient`,
`instT := Con.quotient.decidableEq`, `T' := Multiset`, `instT' := Multiset.decidableEq`,
and `S := Quot`.)

If the type involved is a free variable (rather than an instantiation of some type `S`),
the instance priority should be even lower, see Note [lower instance priority].
-/

-- see Note [slow-failing instance priority]
instance (priority := 900) [DecidableRel (IsConj : Î± â†’ Î± â†’ Prop)] : DecidableEq (ConjClasses Î±) :=
  instDecidableEqQuotient

end Monoid

section CommMonoid

variable [CommMonoid Î±]

theorem mk_injective : Function.Injective (@ConjClasses.mk Î± _) := fun _ _ =>
  (mk_eq_mk_iff_isConj.trans isConj_iff_eq).1
#align conj_classes.mk_injective ConjClasses.mk_injective

theorem mk_bijective : Function.Bijective (@ConjClasses.mk Î± _) :=
  âŸ¨mk_injective, mk_surjectiveâŸ©
#align conj_classes.mk_bijective ConjClasses.mk_bijective

/-- The bijection between a `CommGroup` and its `ConjClasses`. -/
def mkEquiv : Î± â‰ƒ ConjClasses Î± :=
  âŸ¨ConjClasses.mk, Quotient.lift id fun (a : Î±) b => isConj_iff_eq.1, Quotient.lift_mk _ _, by
    rw [Function.RightInverse, Function.LeftInverse, forall_isConj]
    -- âŠ¢ âˆ€ (a : Î±), ConjClasses.mk (Quotient.lift id (_ : âˆ€ (a b : Î±), IsConj a b â†’ a â€¦
    intro x
    -- âŠ¢ ConjClasses.mk (Quotient.lift id (_ : âˆ€ (a b : Î±), IsConj a b â†’ a = b) (Conj â€¦
    rw [â† quotient_mk_eq_mk, â† quotient_mk_eq_mk, Quotient.lift_mk, id.def]âŸ©
    -- ðŸŽ‰ no goals
#align conj_classes.mk_equiv ConjClasses.mkEquiv

end CommMonoid

end ConjClasses

section Monoid

variable [Monoid Î±]

/-- Given an element `a`, `conjugatesOf a` is the set of conjugates. -/
def conjugatesOf (a : Î±) : Set Î± :=
  { b | IsConj a b }
#align conjugates_of conjugatesOf

theorem mem_conjugatesOf_self {a : Î±} : a âˆˆ conjugatesOf a :=
  IsConj.refl _
#align mem_conjugates_of_self mem_conjugatesOf_self

theorem IsConj.conjugatesOf_eq {a b : Î±} (ab : IsConj a b) : conjugatesOf a = conjugatesOf b :=
  Set.ext fun _ => âŸ¨fun ag => ab.symm.trans ag, fun bg => ab.trans bgâŸ©
#align is_conj.conjugates_of_eq IsConj.conjugatesOf_eq

theorem isConj_iff_conjugatesOf_eq {a b : Î±} : IsConj a b â†” conjugatesOf a = conjugatesOf b :=
  âŸ¨IsConj.conjugatesOf_eq, fun h => by
    have ha := @mem_conjugatesOf_self _ _ b -- Porting note: added `@`.
    -- âŠ¢ IsConj a b
    rwa [â† h] at haâŸ©
    -- ðŸŽ‰ no goals
#align is_conj_iff_conjugates_of_eq isConj_iff_conjugatesOf_eq

end Monoid

namespace ConjClasses

variable [Monoid Î±]

attribute [local instance] IsConj.setoid

/-- Given a conjugacy class `a`, `carrier a` is the set it represents. -/
def carrier : ConjClasses Î± â†’ Set Î± :=
  Quotient.lift conjugatesOf fun (_ : Î±) _ ab => IsConj.conjugatesOf_eq ab
#align conj_classes.carrier ConjClasses.carrier

theorem mem_carrier_mk {a : Î±} : a âˆˆ carrier (ConjClasses.mk a) :=
  IsConj.refl _
#align conj_classes.mem_carrier_mk ConjClasses.mem_carrier_mk

theorem mem_carrier_iff_mk_eq {a : Î±} {b : ConjClasses Î±} :
    a âˆˆ carrier b â†” ConjClasses.mk a = b := by
  revert b
  -- âŠ¢ âˆ€ {b : ConjClasses Î±}, a âˆˆ carrier b â†” ConjClasses.mk a = b
  rw [forall_isConj]
  -- âŠ¢ âˆ€ (a_1 : Î±), a âˆˆ carrier (ConjClasses.mk a_1) â†” ConjClasses.mk a = ConjClass â€¦
  intro b
  -- âŠ¢ a âˆˆ carrier (ConjClasses.mk b) â†” ConjClasses.mk a = ConjClasses.mk b
  rw [carrier, eq_comm, mk_eq_mk_iff_isConj, â† quotient_mk_eq_mk, Quotient.lift_mk]
  -- âŠ¢ a âˆˆ conjugatesOf b â†” IsConj b a
  rfl
  -- ðŸŽ‰ no goals
#align conj_classes.mem_carrier_iff_mk_eq ConjClasses.mem_carrier_iff_mk_eq

theorem carrier_eq_preimage_mk {a : ConjClasses Î±} : a.carrier = ConjClasses.mk â»Â¹' {a} :=
  Set.ext fun _ => mem_carrier_iff_mk_eq
#align conj_classes.carrier_eq_preimage_mk ConjClasses.carrier_eq_preimage_mk

end ConjClasses

assert_not_exists Multiset
