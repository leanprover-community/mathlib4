/-
Copyright (c) 2023 Ya√´l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Bhavik Mehta
-/
import Mathlib.Algebra.BigOperators.Expect

/-!
# Balancing a function

This file defines the balancing of a function `f`, defined as `f` minus its average.

This is the unique function `g` such that `f a - f b = g a - g b` for all `a` and `b`, and
`‚àë a, g a = 0`. This is particularly useful in Fourier analysis as `f` and `g` then have the same
Fourier transform, except in the `0`-th frequency where the Fourier transform of `g` vanishes.
-/

open Finset Function
open scoped BigOperators

variable {Œπ H F G : Type*}

namespace Fintype

section AddCommGroup
variable [Fintype Œπ] [AddCommGroup G] [Module ‚Ñö‚â•0 G] [AddCommGroup H] [Module ‚Ñö‚â•0 H]

/-- The balancing of a function, namely the function minus its average. -/
def balance (f : Œπ ‚Üí G) : Œπ ‚Üí G := f - Function.const _ (ùîº y, f y)

lemma balance_apply (f : Œπ ‚Üí G) (x : Œπ) : balance f x = f x - ùîº y, f y := rfl

@[simp] lemma balance_zero : balance (0 : Œπ ‚Üí G) = 0 := by simp [balance]

@[simp] lemma balance_add (f g : Œπ ‚Üí G) : balance (f + g) = balance f + balance g := by
  simp only [balance, expect_add_distrib, ‚Üê const_add, add_sub_add_comm, Pi.add_apply]

@[simp] lemma balance_sub (f g : Œπ ‚Üí G) : balance (f - g) = balance f - balance g := by
  simp only [balance, expect_sub_distrib, const_sub, sub_sub_sub_comm, Pi.sub_apply]

@[simp] lemma balance_neg (f : Œπ ‚Üí G) : balance (-f) = -balance f := by
  simp only [balance, expect_neg_distrib, const_neg, neg_sub', Pi.neg_apply]

@[simp] lemma sum_balance (f : Œπ ‚Üí G) : ‚àë x, balance f x = 0 := by
  cases isEmpty_or_nonempty Œπ <;> simp [balance_apply]

@[simp] lemma expect_balance (f : Œπ ‚Üí G) : ùîº x, balance f x = 0 := by simp [expect]

@[simp] lemma balance_idem (f : Œπ ‚Üí G) : balance (balance f) = balance f := by
  cases isEmpty_or_nonempty Œπ <;> ext x <;> simp [balance, expect_sub_distrib, univ_nonempty]

@[simp] lemma map_balance [FunLike F G H] [LinearMapClass F ‚Ñö‚â•0 G H] (g : F) (f : Œπ ‚Üí G) (a : Œπ) :
    g (balance f a) = balance (g ‚àò f) a := by simp [balance, map_expect]

end AddCommGroup
end Fintype
