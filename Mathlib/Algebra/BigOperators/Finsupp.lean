/-
Copyright (c) 2020 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/
import Mathlib.Data.Finsupp.Indicator
import Mathlib.Algebra.BigOperators.Pi
import Mathlib.Algebra.BigOperators.Ring
import Mathlib.Algebra.BigOperators.Order
import Mathlib.GroupTheory.Submonoid.Membership

#align_import algebra.big_operators.finsupp from "leanprover-community/mathlib"@"842328d9df7e96fd90fc424e115679c15fb23a71"

/-!
# Big operators for finsupps

This file contains theorems relevant to big operators in finitely supported functions.
-/


noncomputable section

open Finset Function

open BigOperators

variable {Î± Î¹ Î³ A B C : Type*} [AddCommMonoid A] [AddCommMonoid B] [AddCommMonoid C]

variable {t : Î¹ â†’ A â†’ C} (h0 : âˆ€ i, t i 0 = 0) (h1 : âˆ€ i x y, t i (x + y) = t i x + t i y)

variable {s : Finset Î±} {f : Î± â†’ Î¹ â†’â‚€ A} (i : Î¹)

variable (g : Î¹ â†’â‚€ A) (k : Î¹ â†’ A â†’ Î³ â†’ B) (x : Î³)

variable {Î² M M' N P G H R S : Type*}

namespace Finsupp

/-!
### Declarations about `sum` and `prod`

In most of this section, the domain `Î²` is assumed to be an `AddMonoid`.
-/


section SumProd

/-- `prod f g` is the product of `g a (f a)` over the support of `f`. -/
@[to_additive "`sum f g` is the sum of `g a (f a)` over the support of `f`. "]
def prod [Zero M] [CommMonoid N] (f : Î± â†’â‚€ M) (g : Î± â†’ M â†’ N) : N :=
  âˆ a in f.support, g a (f a)
#align finsupp.prod Finsupp.prod
#align finsupp.sum Finsupp.sum

variable [Zero M] [Zero M'] [CommMonoid N]

@[to_additive]
theorem prod_of_support_subset (f : Î± â†’â‚€ M) {s : Finset Î±} (hs : f.support âŠ† s) (g : Î± â†’ M â†’ N)
    (h : âˆ€ i âˆˆ s, g i 0 = 1) : f.prod g = âˆ x in s, g x (f x) := by
  refine Finset.prod_subset hs fun x hxs hx => h x hxs â–¸ (congr_arg (g x) ?_)
  -- âŠ¢ â†‘f x = 0
  exact not_mem_support_iff.1 hx
  -- ğŸ‰ no goals
#align finsupp.prod_of_support_subset Finsupp.prod_of_support_subset
#align finsupp.sum_of_support_subset Finsupp.sum_of_support_subset

@[to_additive]
theorem prod_fintype [Fintype Î±] (f : Î± â†’â‚€ M) (g : Î± â†’ M â†’ N) (h : âˆ€ i, g i 0 = 1) :
    f.prod g = âˆ i, g i (f i) :=
  f.prod_of_support_subset (subset_univ _) g fun x _ => h x
#align finsupp.prod_fintype Finsupp.prod_fintype
#align finsupp.sum_fintype Finsupp.sum_fintype

@[to_additive (attr := simp)]
theorem prod_single_index {a : Î±} {b : M} {h : Î± â†’ M â†’ N} (h_zero : h a 0 = 1) :
    (single a b).prod h = h a b :=
  calc
    (single a b).prod h = âˆ x in {a}, h x (single a b x) :=
      prod_of_support_subset _ support_single_subset h fun x hx =>
        (mem_singleton.1 hx).symm â–¸ h_zero
    _ = h a b := by simp
                    -- ğŸ‰ no goals
#align finsupp.prod_single_index Finsupp.prod_single_index
#align finsupp.sum_single_index Finsupp.sum_single_index

@[to_additive]
theorem prod_mapRange_index {f : M â†’ M'} {hf : f 0 = 0} {g : Î± â†’â‚€ M} {h : Î± â†’ M' â†’ N}
    (h0 : âˆ€ a, h a 0 = 1) : (mapRange f hf g).prod h = g.prod fun a b => h a (f b) :=
  Finset.prod_subset support_mapRange fun _ _ H => by rw [not_mem_support_iff.1 H, h0]
                                                      -- ğŸ‰ no goals
#align finsupp.prod_map_range_index Finsupp.prod_mapRange_index
#align finsupp.sum_map_range_index Finsupp.sum_mapRange_index

@[to_additive (attr := simp)]
theorem prod_zero_index {h : Î± â†’ M â†’ N} : (0 : Î± â†’â‚€ M).prod h = 1 :=
  rfl
#align finsupp.prod_zero_index Finsupp.prod_zero_index
#align finsupp.sum_zero_index Finsupp.sum_zero_index

@[to_additive]
theorem prod_comm (f : Î± â†’â‚€ M) (g : Î² â†’â‚€ M') (h : Î± â†’ M â†’ Î² â†’ M' â†’ N) :
    (f.prod fun x v => g.prod fun x' v' => h x v x' v') =
      g.prod fun x' v' => f.prod fun x v => h x v x' v' :=
  Finset.prod_comm
#align finsupp.prod_comm Finsupp.prod_comm
#align finsupp.sum_comm Finsupp.sum_comm

@[to_additive (attr := simp)]
theorem prod_ite_eq [DecidableEq Î±] (f : Î± â†’â‚€ M) (a : Î±) (b : Î± â†’ M â†’ N) :
    (f.prod fun x v => ite (a = x) (b x v) 1) = ite (a âˆˆ f.support) (b a (f a)) 1 := by
  dsimp [Finsupp.prod]
  -- âŠ¢ (âˆ a_1 in f.support, if a = a_1 then b a_1 (â†‘f a_1) else 1) = if a âˆˆ f.suppo â€¦
  rw [f.support.prod_ite_eq]
  -- ğŸ‰ no goals
#align finsupp.prod_ite_eq Finsupp.prod_ite_eq
#align finsupp.sum_ite_eq Finsupp.sum_ite_eq

/- Porting note: simpnf linter, added aux lemma below
Left-hand side simplifies from
  Finsupp.sum f fun x v => if a = x then v else 0
to
  if â†‘f a = 0 then 0 else â†‘f a
-/
-- @[simp]
theorem sum_ite_self_eq [DecidableEq Î±] {N : Type*} [AddCommMonoid N] (f : Î± â†’â‚€ N) (a : Î±) :
    (f.sum fun x v => ite (a = x) v 0) = f a := by
  classical
    convert f.sum_ite_eq a fun _ => id
    simp [ite_eq_right_iff.2 Eq.symm]
#align finsupp.sum_ite_self_eq Finsupp.sum_ite_self_eq

-- Porting note: Added this thm to replace the simp in the previous one. Need to add [DecidableEq N]
@[simp]
theorem sum_ite_self_eq_aux [DecidableEq Î±] {N : Type*} [AddCommMonoid N] (f : Î± â†’â‚€ N) (a : Î±) :
    (if a âˆˆ f.support then f a else 0) = f a := by
  simp only [mem_support_iff, ne_eq, ite_eq_left_iff, not_not]
  -- âŠ¢ â†‘f a = 0 â†’ 0 = â†‘f a
  exact fun h â†¦ h.symm
  -- ğŸ‰ no goals

/-- A restatement of `prod_ite_eq` with the equality test reversed. -/
@[to_additive (attr := simp) "A restatement of `sum_ite_eq` with the equality test reversed."]
theorem prod_ite_eq' [DecidableEq Î±] (f : Î± â†’â‚€ M) (a : Î±) (b : Î± â†’ M â†’ N) :
    (f.prod fun x v => ite (x = a) (b x v) 1) = ite (a âˆˆ f.support) (b a (f a)) 1 := by
  dsimp [Finsupp.prod]
  -- âŠ¢ (âˆ a_1 in f.support, if a_1 = a then b a_1 (â†‘f a_1) else 1) = if a âˆˆ f.suppo â€¦
  rw [f.support.prod_ite_eq']
  -- ğŸ‰ no goals
#align finsupp.prod_ite_eq' Finsupp.prod_ite_eq'
#align finsupp.sum_ite_eq' Finsupp.sum_ite_eq'

-- Porting note: simp can prove this
-- @[simp]
theorem sum_ite_self_eq' [DecidableEq Î±] {N : Type*} [AddCommMonoid N] (f : Î± â†’â‚€ N) (a : Î±) :
    (f.sum fun x v => ite (x = a) v 0) = f a := by
  classical
    convert f.sum_ite_eq' a fun _ => id
    simp [ite_eq_right_iff.2 Eq.symm]
#align finsupp.sum_ite_self_eq' Finsupp.sum_ite_self_eq'

@[simp]
theorem prod_pow [Fintype Î±] (f : Î± â†’â‚€ â„•) (g : Î± â†’ N) :
    (f.prod fun a b => g a ^ b) = âˆ a, g a ^ f a :=
  f.prod_fintype _ fun _ â†¦ pow_zero _
#align finsupp.prod_pow Finsupp.prod_pow

/-- If `g` maps a second argument of 0 to 1, then multiplying it over the
result of `onFinset` is the same as multiplying it over the original `Finset`. -/
@[to_additive
      "If `g` maps a second argument of 0 to 0, summing it over the
      result of `onFinset` is the same as summing it over the original `Finset`."]
theorem onFinset_prod {s : Finset Î±} {f : Î± â†’ M} {g : Î± â†’ M â†’ N} (hf : âˆ€ a, f a â‰  0 â†’ a âˆˆ s)
    (hg : âˆ€ a, g a 0 = 1) : (onFinset s f hf).prod g = âˆ a in s, g a (f a) :=
  Finset.prod_subset support_onFinset_subset <| by simp (config := { contextual := true }) [*]
                                                   -- ğŸ‰ no goals
#align finsupp.on_finset_prod Finsupp.onFinset_prod
#align finsupp.on_finset_sum Finsupp.onFinset_sum

/-- Taking a product over `f : Î± â†’â‚€ M` is the same as multiplying the value on a single element
`y âˆˆ f.support` by the product over `erase y f`. -/
@[to_additive
      " Taking a sum over over `f : Î± â†’â‚€ M` is the same as adding the value on a
      single element `y âˆˆ f.support` to the sum over `erase y f`. "]
theorem mul_prod_erase (f : Î± â†’â‚€ M) (y : Î±) (g : Î± â†’ M â†’ N) (hyf : y âˆˆ f.support) :
    g y (f y) * (erase y f).prod g = f.prod g := by
  classical
    rw [Finsupp.prod, Finsupp.prod, â† Finset.mul_prod_erase _ _ hyf, Finsupp.support_erase,
      Finset.prod_congr rfl]
    intro h hx
    rw [Finsupp.erase_ne (ne_of_mem_erase hx)]
#align finsupp.mul_prod_erase Finsupp.mul_prod_erase
#align finsupp.add_sum_erase Finsupp.add_sum_erase

/-- Generalization of `Finsupp.mul_prod_erase`: if `g` maps a second argument of 0 to 1,
then its product over `f : Î± â†’â‚€ M` is the same as multiplying the value on any element
`y : Î±` by the product over `erase y f`. -/
@[to_additive
      " Generalization of `Finsupp.add_sum_erase`: if `g` maps a second argument of 0
      to 0, then its sum over `f : Î± â†’â‚€ M` is the same as adding the value on any element
      `y : Î±` to the sum over `erase y f`. "]
theorem mul_prod_erase' (f : Î± â†’â‚€ M) (y : Î±) (g : Î± â†’ M â†’ N) (hg : âˆ€ i : Î±, g i 0 = 1) :
    g y (f y) * (erase y f).prod g = f.prod g := by
  classical
    by_cases hyf : y âˆˆ f.support
    Â· exact Finsupp.mul_prod_erase f y g hyf
    Â· rw [not_mem_support_iff.mp hyf, hg y, erase_of_not_mem_support hyf, one_mul]
#align finsupp.mul_prod_erase' Finsupp.mul_prod_erase'
#align finsupp.add_sum_erase' Finsupp.add_sum_erase'

@[to_additive]
theorem _root_.SubmonoidClass.finsupp_prod_mem {S : Type*} [SetLike S N] [SubmonoidClass S N]
    (s : S) (f : Î± â†’â‚€ M) (g : Î± â†’ M â†’ N) (h : âˆ€ c, f c â‰  0 â†’ g c (f c) âˆˆ s) : f.prod g âˆˆ s :=
  prod_mem fun _i hi => h _ (Finsupp.mem_support_iff.mp hi)
#align submonoid_class.finsupp_prod_mem SubmonoidClass.finsupp_prod_mem
#align add_submonoid_class.finsupp_sum_mem AddSubmonoidClass.finsupp_sum_mem

@[to_additive]
theorem prod_congr {f : Î± â†’â‚€ M} {g1 g2 : Î± â†’ M â†’ N} (h : âˆ€ x âˆˆ f.support, g1 x (f x) = g2 x (f x)) :
    f.prod g1 = f.prod g2 :=
  Finset.prod_congr rfl h
#align finsupp.prod_congr Finsupp.prod_congr
#align finsupp.sum_congr Finsupp.sum_congr

end SumProd

end Finsupp

@[to_additive]
theorem map_finsupp_prod [Zero M] [CommMonoid N] [CommMonoid P] {H : Type*} [MonoidHomClass H N P]
    (h : H) (f : Î± â†’â‚€ M) (g : Î± â†’ M â†’ N) : h (f.prod g) = f.prod fun a b => h (g a b) :=
  map_prod h _ _
#align map_finsupp_prod map_finsupp_prod
#align map_finsupp_sum map_finsupp_sum

/-- Deprecated, use `_root_.map_finsupp_prod` instead. -/
@[to_additive (attr := deprecated)
  "Deprecated, use `_root_.map_finsupp_sum` instead."]
protected theorem MulEquiv.map_finsupp_prod [Zero M] [CommMonoid N] [CommMonoid P] (h : N â‰ƒ* P)
    (f : Î± â†’â‚€ M) (g : Î± â†’ M â†’ N) : h (f.prod g) = f.prod fun a b => h (g a b) :=
  map_finsupp_prod h f g
#align mul_equiv.map_finsupp_prod MulEquiv.map_finsupp_prod
#align add_equiv.map_finsupp_sum AddEquiv.map_finsupp_sum

/-- Deprecated, use `_root_.map_finsupp_prod` instead. -/
@[to_additive (attr := deprecated)
  "Deprecated, use `_root_.map_finsupp_sum` instead."]
protected theorem MonoidHom.map_finsupp_prod [Zero M] [CommMonoid N] [CommMonoid P] (h : N â†’* P)
    (f : Î± â†’â‚€ M) (g : Î± â†’ M â†’ N) : h (f.prod g) = f.prod fun a b => h (g a b) :=
  map_finsupp_prod h f g
#align monoid_hom.map_finsupp_prod MonoidHom.map_finsupp_prod
#align add_monoid_hom.map_finsupp_sum AddMonoidHom.map_finsupp_sum

/-- Deprecated, use `_root_.map_finsupp_sum` instead. -/
@[deprecated map_finsupp_sum]
protected theorem RingHom.map_finsupp_sum [Zero M] [Semiring R] [Semiring S] (h : R â†’+* S)
    (f : Î± â†’â‚€ M) (g : Î± â†’ M â†’ R) : h (f.sum g) = f.sum fun a b => h (g a b) :=
  map_finsupp_sum h f g
#align ring_hom.map_finsupp_sum RingHom.map_finsupp_sum

/-- Deprecated, use `_root_.map_finsupp_prod` instead. -/
@[deprecated map_finsupp_prod]
protected theorem RingHom.map_finsupp_prod [Zero M] [CommSemiring R] [CommSemiring S] (h : R â†’+* S)
    (f : Î± â†’â‚€ M) (g : Î± â†’ M â†’ R) : h (f.prod g) = f.prod fun a b => h (g a b) :=
  map_finsupp_prod h f g
#align ring_hom.map_finsupp_prod RingHom.map_finsupp_prod

-- Porting note: inserted â‡‘ on the rhs
@[to_additive]
theorem MonoidHom.coe_finsupp_prod [Zero Î²] [Monoid N] [CommMonoid P] (f : Î± â†’â‚€ Î²)
    (g : Î± â†’ Î² â†’ N â†’* P) : â‡‘(f.prod g) = f.prod fun i fi => â‡‘(g i fi) :=
  MonoidHom.coe_finset_prod _ _
#align monoid_hom.coe_finsupp_prod MonoidHom.coe_finsupp_prod
#align add_monoid_hom.coe_finsupp_sum AddMonoidHom.coe_finsupp_sum

@[to_additive (attr := simp)]
theorem MonoidHom.finsupp_prod_apply [Zero Î²] [Monoid N] [CommMonoid P] (f : Î± â†’â‚€ Î²)
    (g : Î± â†’ Î² â†’ N â†’* P) (x : N) : f.prod g x = f.prod fun i fi => g i fi x :=
  MonoidHom.finset_prod_apply _ _ _
#align monoid_hom.finsupp_prod_apply MonoidHom.finsupp_prod_apply
#align add_monoid_hom.finsupp_sum_apply AddMonoidHom.finsupp_sum_apply

namespace Finsupp

theorem single_multiset_sum [AddCommMonoid M] (s : Multiset M) (a : Î±) :
    single a s.sum = (s.map (single a)).sum :=
  Multiset.induction_on s (single_zero _) fun a s ih => by
    rw [Multiset.sum_cons, single_add, ih, Multiset.map_cons, Multiset.sum_cons]
    -- ğŸ‰ no goals
#align finsupp.single_multiset_sum Finsupp.single_multiset_sum

theorem single_finset_sum [AddCommMonoid M] (s : Finset Î¹) (f : Î¹ â†’ M) (a : Î±) :
    single a (âˆ‘ b in s, f b) = âˆ‘ b in s, single a (f b) := by
  trans
  Â· apply single_multiset_sum
    -- ğŸ‰ no goals
  Â· rw [Multiset.map_map]
    -- âŠ¢ Multiset.sum (Multiset.map (single a âˆ˜ fun b => f b) s.val) = âˆ‘ b in s, sing â€¦
    rfl
    -- ğŸ‰ no goals
#align finsupp.single_finset_sum Finsupp.single_finset_sum

theorem single_sum [Zero M] [AddCommMonoid N] (s : Î¹ â†’â‚€ M) (f : Î¹ â†’ M â†’ N) (a : Î±) :
    single a (s.sum f) = s.sum fun d c => single a (f d c) :=
  single_finset_sum _ _ _
#align finsupp.single_sum Finsupp.single_sum

@[to_additive]
theorem prod_neg_index [AddGroup G] [CommMonoid M] {g : Î± â†’â‚€ G} {h : Î± â†’ G â†’ M}
    (h0 : âˆ€ a, h a 0 = 1) : (-g).prod h = g.prod fun a b => h a (-b) :=
  prod_mapRange_index h0
#align finsupp.prod_neg_index Finsupp.prod_neg_index
#align finsupp.sum_neg_index Finsupp.sum_neg_index

end Finsupp

namespace Finsupp

theorem finset_sum_apply [AddCommMonoid N] (S : Finset Î¹) (f : Î¹ â†’ Î± â†’â‚€ N) (a : Î±) :
    (âˆ‘ i in S, f i) a = âˆ‘ i in S, f i a :=
  (applyAddHom a : (Î± â†’â‚€ N) â†’+ _).map_sum _ _
#align finsupp.finset_sum_apply Finsupp.finset_sum_apply

@[simp]
theorem sum_apply [Zero M] [AddCommMonoid N] {f : Î± â†’â‚€ M} {g : Î± â†’ M â†’ Î² â†’â‚€ N} {aâ‚‚ : Î²} :
    (f.sum g) aâ‚‚ = f.sum fun aâ‚ b => g aâ‚ b aâ‚‚ :=
  finset_sum_apply _ _ _
#align finsupp.sum_apply Finsupp.sum_apply

-- Porting note: inserted â‡‘ on the rhs
theorem coe_finset_sum [AddCommMonoid N] (S : Finset Î¹) (f : Î¹ â†’ Î± â†’â‚€ N) :
    â‡‘(âˆ‘ i in S, f i) = âˆ‘ i in S, â‡‘(f i) :=
  (coeFnAddHom : (Î± â†’â‚€ N) â†’+ _).map_sum _ _
#align finsupp.coe_finset_sum Finsupp.coe_finset_sum

-- Porting note: inserted â‡‘ on the rhs
theorem coe_sum [Zero M] [AddCommMonoid N] (f : Î± â†’â‚€ M) (g : Î± â†’ M â†’ Î² â†’â‚€ N) :
    â‡‘(f.sum g) = f.sum fun aâ‚ b => â‡‘(g aâ‚ b) :=
  coe_finset_sum _ _
#align finsupp.coe_sum Finsupp.coe_sum

theorem support_sum [DecidableEq Î²] [Zero M] [AddCommMonoid N] {f : Î± â†’â‚€ M} {g : Î± â†’ M â†’ Î² â†’â‚€ N} :
    (f.sum g).support âŠ† f.support.biUnion fun a => (g a (f a)).support := by
  have : âˆ€ c, (f.sum fun a b => g a b c) â‰  0 â†’ âˆƒ a, f a â‰  0 âˆ§ Â¬(g a (f a)) c = 0 := fun aâ‚ h =>
    let âŸ¨a, ha, neâŸ© := Finset.exists_ne_zero_of_sum_ne_zero h
    âŸ¨a, mem_support_iff.mp ha, neâŸ©
  simpa only [Finset.subset_iff, mem_support_iff, Finset.mem_biUnion, sum_apply, exists_prop]
  -- ğŸ‰ no goals
#align finsupp.support_sum Finsupp.support_sum

theorem support_finset_sum [DecidableEq Î²] [AddCommMonoid M] {s : Finset Î±} {f : Î± â†’ Î² â†’â‚€ M} :
    (Finset.sum s f).support âŠ† s.biUnion fun x => (f x).support := by
  rw [â† Finset.sup_eq_biUnion]
  -- âŠ¢ (Finset.sum s f).support âŠ† sup s fun x => (f x).support
  induction' s using Finset.cons_induction_on with a s ha ih
  -- âŠ¢ (Finset.sum âˆ… f).support âŠ† sup âˆ… fun x => (f x).support
  Â· rfl
    -- ğŸ‰ no goals
  Â· rw [Finset.sum_cons, Finset.sup_cons]
    -- âŠ¢ (f a + âˆ‘ x in s, f x).support âŠ† (f a).support âŠ” sup s fun x => (f x).support
    exact support_add.trans (Finset.union_subset_union (Finset.Subset.refl _) ih)
    -- ğŸ‰ no goals
#align finsupp.support_finset_sum Finsupp.support_finset_sum

@[simp]
theorem sum_zero [Zero M] [AddCommMonoid N] {f : Î± â†’â‚€ M} : (f.sum fun _ _ => (0 : N)) = 0 :=
  Finset.sum_const_zero
#align finsupp.sum_zero Finsupp.sum_zero

@[to_additive (attr := simp)]
theorem prod_mul [Zero M] [CommMonoid N] {f : Î± â†’â‚€ M} {hâ‚ hâ‚‚ : Î± â†’ M â†’ N} :
    (f.prod fun a b => hâ‚ a b * hâ‚‚ a b) = f.prod hâ‚ * f.prod hâ‚‚ :=
  Finset.prod_mul_distrib
#align finsupp.prod_mul Finsupp.prod_mul
#align finsupp.sum_add Finsupp.sum_add

@[to_additive (attr := simp)]
theorem prod_inv [Zero M] [CommGroup G] {f : Î± â†’â‚€ M} {h : Î± â†’ M â†’ G} :
    (f.prod fun a b => (h a b)â»Â¹) = (f.prod h)â»Â¹ :=
  (map_prod (MonoidHom.id G)â»Â¹ _ _).symm
#align finsupp.prod_inv Finsupp.prod_inv
#align finsupp.sum_neg Finsupp.sum_neg

@[simp]
theorem sum_sub [Zero M] [AddCommGroup G] {f : Î± â†’â‚€ M} {hâ‚ hâ‚‚ : Î± â†’ M â†’ G} :
    (f.sum fun a b => hâ‚ a b - hâ‚‚ a b) = f.sum hâ‚ - f.sum hâ‚‚ :=
  Finset.sum_sub_distrib
#align finsupp.sum_sub Finsupp.sum_sub

/-- Taking the product under `h` is an additive-to-multiplicative homomorphism of finsupps,
if `h` is an additive-to-multiplicative homomorphism on the support.
This is a more general version of `Finsupp.prod_add_index'`; the latter has simpler hypotheses. -/
@[to_additive
      "Taking the product under `h` is an additive homomorphism of finsupps,  if `h` is an
      additive homomorphism on the support. This is a more general version of
      `Finsupp.sum_add_index'`; the latter has simpler hypotheses."]
theorem prod_add_index [DecidableEq Î±] [AddZeroClass M] [CommMonoid N] {f g : Î± â†’â‚€ M}
    {h : Î± â†’ M â†’ N} (h_zero : âˆ€ a âˆˆ f.support âˆª g.support, h a 0 = 1)
    (h_add : âˆ€ a âˆˆ f.support âˆª g.support, âˆ€ (bâ‚ bâ‚‚), h a (bâ‚ + bâ‚‚) = h a bâ‚ * h a bâ‚‚) :
    (f + g).prod h = f.prod h * g.prod h := by
  rw [Finsupp.prod_of_support_subset f (subset_union_left _ g.support) h h_zero,
    Finsupp.prod_of_support_subset g (subset_union_right f.support _) h h_zero, â†
    Finset.prod_mul_distrib, Finsupp.prod_of_support_subset (f + g) Finsupp.support_add h h_zero]
  exact Finset.prod_congr rfl fun x hx => by apply h_add x hx
  -- ğŸ‰ no goals
#align finsupp.prod_add_index Finsupp.prod_add_index
#align finsupp.sum_add_index Finsupp.sum_add_index

/-- Taking the product under `h` is an additive-to-multiplicative homomorphism of finsupps,
if `h` is an additive-to-multiplicative homomorphism.
This is a more specialized version of `Finsupp.prod_add_index` with simpler hypotheses. -/
@[to_additive
      "Taking the sum under `h` is an additive homomorphism of finsupps,if `h` is an additive
      homomorphism. This is a more specific version of `finsupp.sum_add_index` with simpler
      hypotheses."]
theorem prod_add_index' [AddZeroClass M] [CommMonoid N] {f g : Î± â†’â‚€ M} {h : Î± â†’ M â†’ N}
    (h_zero : âˆ€ a, h a 0 = 1) (h_add : âˆ€ a bâ‚ bâ‚‚, h a (bâ‚ + bâ‚‚) = h a bâ‚ * h a bâ‚‚) :
    (f + g).prod h = f.prod h * g.prod h := by
  classical exact prod_add_index (fun a _ => h_zero a) fun a _ => h_add a
  -- ğŸ‰ no goals
#align finsupp.prod_add_index' Finsupp.prod_add_index'
#align finsupp.sum_add_index' Finsupp.sum_add_index'

@[simp]
theorem sum_hom_add_index [AddZeroClass M] [AddCommMonoid N] {f g : Î± â†’â‚€ M} (h : Î± â†’ M â†’+ N) :
    ((f + g).sum fun x => h x) = (f.sum fun x => h x) + g.sum fun x => h x :=
  sum_add_index' (fun a => (h a).map_zero) fun a => (h a).map_add
#align finsupp.sum_hom_add_index Finsupp.sum_hom_add_index

@[simp]
theorem prod_hom_add_index [AddZeroClass M] [CommMonoid N] {f g : Î± â†’â‚€ M}
    (h : Î± â†’ Multiplicative M â†’* N) :
    ((f + g).prod fun a b => h a (Multiplicative.ofAdd b)) =
      (f.prod fun a b => h a (Multiplicative.ofAdd b)) *
        g.prod fun a b => h a (Multiplicative.ofAdd b) :=
  prod_add_index' (fun a => (h a).map_one) fun a => (h a).map_mul
#align finsupp.prod_hom_add_index Finsupp.prod_hom_add_index

/-- The canonical isomorphism between families of additive monoid homomorphisms `Î± â†’ (M â†’+ N)`
and monoid homomorphisms `(Î± â†’â‚€ M) â†’+ N`. -/
def liftAddHom [AddZeroClass M] [AddCommMonoid N] : (Î± â†’ M â†’+ N) â‰ƒ+ ((Î± â†’â‚€ M) â†’+ N)
    where
  toFun F :=
    { toFun := fun f â†¦ f.sum fun x â†¦ F x
      map_zero' := Finset.sum_empty
      map_add' := fun _ _ => sum_add_index' (fun x => (F x).map_zero) fun x => (F x).map_add }
  invFun F x := F.comp (singleAddHom x)
  left_inv F := by
    ext
    -- âŠ¢ â†‘((fun F x => AddMonoidHom.comp F (singleAddHom x)) ((fun F => { toZeroHom : â€¦
    simp [singleAddHom]
    -- ğŸ‰ no goals
  right_inv F := by
  -- Porting note: This was `ext` and used the wrong lemma
    apply Finsupp.addHom_ext'
    -- âŠ¢ âˆ€ (x : Î±), AddMonoidHom.comp ((fun F => { toZeroHom := { toFun := fun f => s â€¦
    simp [singleAddHom, AddMonoidHom.comp, Function.comp]
    -- ğŸ‰ no goals
  map_add' F G := by
    ext x
    -- âŠ¢ â†‘(AddMonoidHom.comp (Equiv.toFun { toFun := fun F => { toZeroHom := { toFun  â€¦
    exact sum_add
    -- ğŸ‰ no goals
#align finsupp.lift_add_hom Finsupp.liftAddHom

@[simp]
theorem liftAddHom_apply [AddCommMonoid M] [AddCommMonoid N] (F : Î± â†’ M â†’+ N) (f : Î± â†’â‚€ M) :
    (liftAddHom (Î± := Î±) (M := M) (N := N)) F f = f.sum fun x => F x :=
  rfl
#align finsupp.lift_add_hom_apply Finsupp.liftAddHom_apply

@[simp]
theorem liftAddHom_symm_apply [AddCommMonoid M] [AddCommMonoid N] (F : (Î± â†’â‚€ M) â†’+ N) (x : Î±) :
    (liftAddHom (Î± := Î±) (M := M) (N := N)).symm F x = F.comp (singleAddHom x) :=
  rfl
#align finsupp.lift_add_hom_symm_apply Finsupp.liftAddHom_symm_apply

theorem liftAddHom_symm_apply_apply [AddCommMonoid M] [AddCommMonoid N] (F : (Î± â†’â‚€ M) â†’+ N) (x : Î±)
    (y : M) : (liftAddHom (Î± := Î±) (M := M) (N := N)).symm F x y = F (single x y) :=
  rfl
#align finsupp.lift_add_hom_symm_apply_apply Finsupp.liftAddHom_symm_apply_apply

@[simp]
theorem liftAddHom_singleAddHom [AddCommMonoid M] :
    (liftAddHom (Î± := Î±) (M := M) (N := Î± â†’â‚€ M)) (singleAddHom : Î± â†’ M â†’+ Î± â†’â‚€ M) =
      AddMonoidHom.id _ :=
  liftAddHom.toEquiv.apply_eq_iff_eq_symm_apply.2 rfl
#align finsupp.lift_add_hom_single_add_hom Finsupp.liftAddHom_singleAddHom

@[simp]
theorem sum_single [AddCommMonoid M] (f : Î± â†’â‚€ M) : f.sum single = f :=
  FunLike.congr_fun liftAddHom_singleAddHom f
#align finsupp.sum_single Finsupp.sum_single

@[simp]
theorem sum_univ_single [AddCommMonoid M] [Fintype Î±] (i : Î±) (m : M) :
    (âˆ‘ j : Î±, (single i m) j) = m := by
-- Porting note: rewrite due to leaky classical in lean3
  classical rw [single, coe_mk, Finset.sum_pi_single']
  -- âŠ¢ (if i âˆˆ univ then m else 0) = m
  simp
  -- ğŸ‰ no goals
#align finsupp.sum_univ_single Finsupp.sum_univ_single

@[simp]
theorem sum_univ_single' [AddCommMonoid M] [Fintype Î±] (i : Î±) (m : M) :
    (âˆ‘ j : Î±, (single j m) i) = m := by
-- Porting note: rewrite due to leaky classical in lean3
  simp_rw [single, coe_mk]
  -- âŠ¢ âˆ‘ x : Î±, Pi.single x m i = m
  classical rw [Finset.sum_pi_single]
  -- âŠ¢ (if i âˆˆ univ then m else 0) = m
  simp
  -- ğŸ‰ no goals
#align finsupp.sum_univ_single' Finsupp.sum_univ_single'

-- Porting note: simp can prove this
-- @[simp]
theorem liftAddHom_apply_single [AddCommMonoid M] [AddCommMonoid N] (f : Î± â†’ M â†’+ N) (a : Î±)
    (b : M) : (liftAddHom (Î± := Î±) (M := M) (N := N)) f (single a b) = f a b :=
  sum_single_index (f a).map_zero
#align finsupp.lift_add_hom_apply_single Finsupp.liftAddHom_apply_single

@[simp]
theorem liftAddHom_comp_single [AddCommMonoid M] [AddCommMonoid N] (f : Î± â†’ M â†’+ N) (a : Î±) :
    ((liftAddHom (Î± := Î±) (M := M) (N := N)) f).comp (singleAddHom a) = f a :=
  AddMonoidHom.ext fun b => liftAddHom_apply_single f a b
#align finsupp.lift_add_hom_comp_single Finsupp.liftAddHom_comp_single

theorem comp_liftAddHom [AddCommMonoid M] [AddCommMonoid N] [AddCommMonoid P] (g : N â†’+ P)
    (f : Î± â†’ M â†’+ N) :
    g.comp ((liftAddHom (Î± := Î±) (M := M) (N := N)) f) =
      (liftAddHom (Î± := Î±) (M := M) (N := P)) fun a => g.comp (f a) :=
  liftAddHom.symm_apply_eq.1 <|
    funext fun a => by
      rw [liftAddHom_symm_apply, AddMonoidHom.comp_assoc, liftAddHom_comp_single]
      -- ğŸ‰ no goals
#align finsupp.comp_lift_add_hom Finsupp.comp_liftAddHom

theorem sum_sub_index [AddCommGroup Î²] [AddCommGroup Î³] {f g : Î± â†’â‚€ Î²} {h : Î± â†’ Î² â†’ Î³}
    (h_sub : âˆ€ a bâ‚ bâ‚‚, h a (bâ‚ - bâ‚‚) = h a bâ‚ - h a bâ‚‚) : (f - g).sum h = f.sum h - g.sum h :=
  ((liftAddHom (Î± := Î±) (M := Î²) (N := Î³)) fun a =>
    AddMonoidHom.ofMapSub (h a) (h_sub a)).map_sub f g
#align finsupp.sum_sub_index Finsupp.sum_sub_index

@[to_additive]
theorem prod_embDomain [Zero M] [CommMonoid N] {v : Î± â†’â‚€ M} {f : Î± â†ª Î²} {g : Î² â†’ M â†’ N} :
    (v.embDomain f).prod g = v.prod fun a b => g (f a) b := by
  rw [prod, prod, support_embDomain, Finset.prod_map]
  -- âŠ¢ âˆ x in v.support, g (â†‘f x) (â†‘(embDomain f v) (â†‘f x)) = âˆ a in v.support, g ( â€¦
  simp_rw [embDomain_apply]
  -- ğŸ‰ no goals
#align finsupp.prod_emb_domain Finsupp.prod_embDomain
#align finsupp.sum_emb_domain Finsupp.sum_embDomain

@[to_additive]
theorem prod_finset_sum_index [AddCommMonoid M] [CommMonoid N] {s : Finset Î¹} {g : Î¹ â†’ Î± â†’â‚€ M}
    {h : Î± â†’ M â†’ N} (h_zero : âˆ€ a, h a 0 = 1) (h_add : âˆ€ a bâ‚ bâ‚‚, h a (bâ‚ + bâ‚‚) = h a bâ‚ * h a bâ‚‚) :
    (âˆ i in s, (g i).prod h) = (âˆ‘ i in s, g i).prod h :=
  Finset.cons_induction_on s rfl fun a s has ih => by
    rw [prod_cons, ih, sum_cons, prod_add_index' h_zero h_add]
    -- ğŸ‰ no goals
#align finsupp.prod_finset_sum_index Finsupp.prod_finset_sum_index
#align finsupp.sum_finset_sum_index Finsupp.sum_finset_sum_index

@[to_additive]
theorem prod_sum_index [AddCommMonoid M] [AddCommMonoid N] [CommMonoid P] {f : Î± â†’â‚€ M}
    {g : Î± â†’ M â†’ Î² â†’â‚€ N} {h : Î² â†’ N â†’ P} (h_zero : âˆ€ a, h a 0 = 1)
    (h_add : âˆ€ a bâ‚ bâ‚‚, h a (bâ‚ + bâ‚‚) = h a bâ‚ * h a bâ‚‚) :
    (f.sum g).prod h = f.prod fun a b => (g a b).prod h :=
  (prod_finset_sum_index h_zero h_add).symm
#align finsupp.prod_sum_index Finsupp.prod_sum_index
#align finsupp.sum_sum_index Finsupp.sum_sum_index

theorem multiset_sum_sum_index [AddCommMonoid M] [AddCommMonoid N] (f : Multiset (Î± â†’â‚€ M))
    (h : Î± â†’ M â†’ N) (hâ‚€ : âˆ€ a, h a 0 = 0)
    (hâ‚ : âˆ€ (a : Î±) (bâ‚ bâ‚‚ : M), h a (bâ‚ + bâ‚‚) = h a bâ‚ + h a bâ‚‚) :
    f.sum.sum h = (f.map fun g : Î± â†’â‚€ M => g.sum h).sum :=
  Multiset.induction_on f rfl fun a s ih => by
    rw [Multiset.sum_cons, Multiset.map_cons, Multiset.sum_cons, sum_add_index' hâ‚€ hâ‚, ih]
    -- ğŸ‰ no goals
#align finsupp.multiset_sum_sum_index Finsupp.multiset_sum_sum_index

theorem support_sum_eq_biUnion {Î± : Type*} {Î¹ : Type*} {M : Type*} [DecidableEq Î±]
    [AddCommMonoid M] {g : Î¹ â†’ Î± â†’â‚€ M} (s : Finset Î¹)
    (h : âˆ€ iâ‚ iâ‚‚, iâ‚ â‰  iâ‚‚ â†’ Disjoint (g iâ‚).support (g iâ‚‚).support) :
    (âˆ‘ i in s, g i).support = s.biUnion fun i => (g i).support := by
  classical
  -- Porting note: apply Finset.induction_on s was not working; refine does.
  refine Finset.induction_on s ?_ ?_
  Â· simp
  Â· intro i s hi
    simp only [hi, sum_insert, not_false_iff, biUnion_insert]
    intro hs
    rw [Finsupp.support_add_eq, hs]
    rw [hs, Finset.disjoint_biUnion_right]
    intro j hj
    refine' h _ _ (ne_of_mem_of_not_mem hj hi).symm
#align finsupp.support_sum_eq_bUnion Finsupp.support_sum_eq_biUnion

theorem multiset_map_sum [Zero M] {f : Î± â†’â‚€ M} {m : Î² â†’ Î³} {h : Î± â†’ M â†’ Multiset Î²} :
    Multiset.map m (f.sum h) = f.sum fun a b => (h a b).map m :=
  (Multiset.mapAddMonoidHom m).map_sum _ f.support
#align finsupp.multiset_map_sum Finsupp.multiset_map_sum

theorem multiset_sum_sum [Zero M] [AddCommMonoid N] {f : Î± â†’â‚€ M} {h : Î± â†’ M â†’ Multiset N} :
    Multiset.sum (f.sum h) = f.sum fun a b => Multiset.sum (h a b) :=
  (Multiset.sumAddMonoidHom : Multiset N â†’+ N).map_sum _ f.support
#align finsupp.multiset_sum_sum Finsupp.multiset_sum_sum

/-- For disjoint `f1` and `f2`, and function `g`, the product of the products of `g`
over `f1` and `f2` equals the product of `g` over `f1 + f2` -/
@[to_additive
      "For disjoint `f1` and `f2`, and function `g`, the sum of the sums of `g`
      over `f1` and `f2` equals the sum of `g` over `f1 + f2`"]
theorem prod_add_index_of_disjoint [AddCommMonoid M] {f1 f2 : Î± â†’â‚€ M}
    (hd : Disjoint f1.support f2.support) {Î² : Type*} [CommMonoid Î²] (g : Î± â†’ M â†’ Î²) :
    (f1 + f2).prod g = f1.prod g * f2.prod g := by
  have :
    âˆ€ {f1 f2 : Î± â†’â‚€ M},
      Disjoint f1.support f2.support â†’ (âˆ x in f1.support, g x (f1 x + f2 x)) = f1.prod g :=
    fun hd =>
    Finset.prod_congr rfl fun x hx => by
      simp only [not_mem_support_iff.mp (disjoint_left.mp hd hx), add_zero]
  classical simp_rw [â† this hd, â† this hd.symm, add_comm (f2 _), Finsupp.prod, support_add_eq hd,
      prod_union hd, add_apply]
#align finsupp.prod_add_index_of_disjoint Finsupp.prod_add_index_of_disjoint
#align finsupp.sum_add_index_of_disjoint Finsupp.sum_add_index_of_disjoint

theorem prod_dvd_prod_of_subset_of_dvd [AddCommMonoid M] [CommMonoid N] {f1 f2 : Î± â†’â‚€ M}
    {g1 g2 : Î± â†’ M â†’ N} (h1 : f1.support âŠ† f2.support)
    (h2 : âˆ€ a : Î±, a âˆˆ f1.support â†’ g1 a (f1 a) âˆ£ g2 a (f2 a)) : f1.prod g1 âˆ£ f2.prod g2 := by
  classical
    simp only [Finsupp.prod, Finsupp.prod_mul]
    rw [â† sdiff_union_of_subset h1, prod_union sdiff_disjoint]
    apply dvd_mul_of_dvd_right
    apply prod_dvd_prod_of_dvd
    exact h2
#align finsupp.prod_dvd_prod_of_subset_of_dvd Finsupp.prod_dvd_prod_of_subset_of_dvd

lemma indicator_eq_sum_attach_single [AddCommMonoid M] {s : Finset Î±} (f : âˆ€ a âˆˆ s, M) :
    indicator s f = âˆ‘ x in s.attach, single â†‘x (f x x.2) := by
  rw [â† sum_single (indicator s f), sum, sum_subset (support_indicator_subset _ _), â† sum_attach]
  -- âŠ¢ âˆ‘ x in attach s, single (â†‘x) (â†‘(indicator s f) â†‘x) = âˆ‘ x in attach s, single â€¦
  Â· refine' Finset.sum_congr rfl (fun _ _ => _)
    -- âŠ¢ single (â†‘xâœÂ¹) (â†‘(indicator s f) â†‘xâœÂ¹) = single (â†‘xâœÂ¹) (f â†‘xâœÂ¹ (_ : â†‘xâœÂ¹ âˆˆ s))
    rw [indicator_of_mem]
    -- ğŸ‰ no goals
  Â· intro i _ hi
    -- âŠ¢ single i (â†‘(indicator s f) i) = 0
    rw [not_mem_support_iff.mp hi, single_zero]
    -- ğŸ‰ no goals
#align finsupp.indicator_eq_sum_single Finsupp.indicator_eq_sum_attach_single

lemma indicator_eq_sum_single [AddCommMonoid M] (s : Finset Î±) (f : Î± â†’ M) :
    indicator s (fun x _ â†¦ f x) = âˆ‘ x in s, single x (f x) :=
  (indicator_eq_sum_attach_single _).trans <| sum_attach (f := fun x â†¦ single x (f x))

@[to_additive (attr := simp)]
lemma prod_indicator_index_eq_prod_attach [Zero M] [CommMonoid N]
    {s : Finset Î±} (f : âˆ€ a âˆˆ s, M) {h : Î± â†’ M â†’ N} (h_zero : âˆ€ a âˆˆ s, h a 0 = 1) :
    (indicator s f).prod h = âˆ x in s.attach, h â†‘x (f x x.2) := by
  rw [prod_of_support_subset _ (support_indicator_subset _ _) h h_zero, â† prod_attach]
  -- âŠ¢ âˆ x in attach s, h (â†‘x) (â†‘(indicator s f) â†‘x) = âˆ x in attach s, h (â†‘x) (f â†‘ â€¦
  refine' Finset.prod_congr rfl (fun _ _ => _)
  -- âŠ¢ h (â†‘xâœÂ¹) (â†‘(indicator s f) â†‘xâœÂ¹) = h (â†‘xâœÂ¹) (f â†‘xâœÂ¹ (_ : â†‘xâœÂ¹ âˆˆ s))
  rw [indicator_of_mem]
  -- ğŸ‰ no goals
#align finsupp.prod_indicator_index Finsupp.prod_indicator_index_eq_prod_attach
#align finsupp.sum_indicator_index Finsupp.sum_indicator_index_eq_sum_attach

@[to_additive (attr := simp)]
lemma prod_indicator_index [Zero M] [CommMonoid N]
    {s : Finset Î±} (f : Î± â†’ M) {h : Î± â†’ M â†’ N} (h_zero : âˆ€ a âˆˆ s, h a 0 = 1) :
    (indicator s (fun x _ â†¦ f x)).prod h = âˆ x in s, h x (f x) :=
  (prod_indicator_index_eq_prod_attach _ h_zero).trans <| prod_attach (f := fun x â†¦ h x (f x))

end Finsupp

theorem Finset.sum_apply' : (âˆ‘ k in s, f k) i = âˆ‘ k in s, f k i :=
  (Finsupp.applyAddHom i : (Î¹ â†’â‚€ A) â†’+ A).map_sum f s
#align finset.sum_apply' Finset.sum_apply'

theorem Finsupp.sum_apply' : g.sum k x = g.sum fun i b => k i b x :=
  Finset.sum_apply _ _ _
#align finsupp.sum_apply' Finsupp.sum_apply'

section

open Classical

theorem Finsupp.sum_sum_index' : (âˆ‘ x in s, f x).sum t = âˆ‘ x in s, (f x).sum t :=
  Finset.induction_on s rfl fun a s has ih => by
    simp_rw [Finset.sum_insert has, Finsupp.sum_add_index' h0 h1, ih]
    -- ğŸ‰ no goals
#align finsupp.sum_sum_index' Finsupp.sum_sum_index'

end

section

variable [NonUnitalNonAssocSemiring R] [NonUnitalNonAssocSemiring S]

theorem Finsupp.sum_mul (b : S) (s : Î± â†’â‚€ R) {f : Î± â†’ R â†’ S} :
    s.sum f * b = s.sum fun a c => f a c * b := by simp only [Finsupp.sum, Finset.sum_mul]
                                                   -- ğŸ‰ no goals
#align finsupp.sum_mul Finsupp.sum_mul

theorem Finsupp.mul_sum (b : S) (s : Î± â†’â‚€ R) {f : Î± â†’ R â†’ S} :
    b * s.sum f = s.sum fun a c => b * f a c := by simp only [Finsupp.sum, Finset.mul_sum]
                                                   -- ğŸ‰ no goals
#align finsupp.mul_sum Finsupp.mul_sum

end

namespace Nat

-- Porting note: Needed to replace pow with (Â· ^ Â·)
/-- If `0 : â„•` is not in the support of `f : â„• â†’â‚€ â„•` then `0 < âˆ x in f.support, x ^ (f x)`. -/
theorem prod_pow_pos_of_zero_not_mem_support {f : â„• â†’â‚€ â„•} (hf : 0 âˆ‰ f.support) :
    0 < f.prod (Â· ^ Â·) :=
 Finset.prod_pos fun a ha => pos_iff_ne_zero.mpr (pow_ne_zero _ fun H => by subst H; exact hf ha)
                                                                            -- âŠ¢ False
                                                                                     -- ğŸ‰ no goals
#align nat.prod_pow_pos_of_zero_not_mem_support Nat.prod_pow_pos_of_zero_not_mem_support

end Nat
