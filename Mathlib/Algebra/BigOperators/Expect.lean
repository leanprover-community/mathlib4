/-
Copyright (c) 2024 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import Mathlib.Algebra.Algebra.Rat
import Mathlib.Algebra.BigOperators.GroupWithZero.Action
import Mathlib.Algebra.BigOperators.Pi
import Mathlib.Algebra.BigOperators.Ring.Finset
import Mathlib.Algebra.Module.Pi
import Mathlib.Data.Finset.Density
import Mathlib.Data.Fintype.BigOperators
import Mathlib.Algebra.Group.Pointwise.Finset.Basic

/-!
# Average over a finset

This file defines `Finset.expect`, the average (aka expectation) of a function over a finset.

## Notation

* `ğ”¼ i âˆˆ s, f i` is notation for `Finset.expect s f`. It is the expectation of `f i` where `i`
  ranges over the finite set `s` (either a `Finset` or a `Set` with a `Fintype` instance).
* `ğ”¼ i, f i` is notation for `Finset.expect Finset.univ f`. It is the expectation of `f i` where `i`
  ranges over the finite domain of `f`.
* `ğ”¼ i âˆˆ s with p i, f i` is notation for `Finset.expect (Finset.filter p s) f`. This is referred to
  as `expectWith` in lemma names.
* `ğ”¼ (i âˆˆ s) (j âˆˆ t), f i j` is notation for `Finset.expect (s Ã—Ë¢ t) (fun âŸ¨i, jâŸ© â†¦ f i j)`.

## Implementation notes

This definition is a special case of the general convex combination operator in a convex space.
However:
1. We don't yet have general convex spaces.
2. The uniform weights case is an overwhelmingly useful special case which should have its own API.

When convex spaces are finally defined, we should redefine `Finset.expect` in terms of that convex
combination operator.

## TODO

* Connect `Finset.expect` with the expectation over `s` in the probability theory sense.
* Give a formulation of Jensen's inequality in this language.
-/

open Finset Function
open Fintype (card)
open scoped Pointwise

variable {Î¹ Îº M N : Type*}

local notation a " /â„š " q => (q : â„šâ‰¥0)â»Â¹ â€¢ a

/-- Average of a function over a finset. If the finset is empty, this is equal to zero. -/
def Finset.expect [AddCommMonoid M] [Module â„šâ‰¥0 M] (s : Finset Î¹) (f : Î¹ â†’ M) : M :=
  (#s : â„šâ‰¥0)â»Â¹ â€¢ âˆ‘ i âˆˆ s, f i

namespace BigOperators
open Batteries.ExtendedBinder Lean Meta

/--
* `ğ”¼ i âˆˆ s, f i` is notation for `Finset.expect s f`. It is the expectation of `f i` where `i`
  ranges over the finite set `s` (either a `Finset` or a `Set` with a `Fintype` instance).
* `ğ”¼ i, f i` is notation for `Finset.expect Finset.univ f`. It is the expectation of `f i` where `i`
  ranges over the finite domain of `f`.
* `ğ”¼ i âˆˆ s with p i, f i` is notation for `Finset.expect (Finset.filter p s) f`.
* `ğ”¼ (i âˆˆ s) (j âˆˆ t), f i j` is notation for `Finset.expect (s Ã—Ë¢ t) (fun âŸ¨i, jâŸ© â†¦ f i j)`.

These support destructuring, for example `ğ”¼ âŸ¨i, jâŸ© âˆˆ s Ã—Ë¢ t, f i j`.

Notation: `"ğ”¼" bigOpBinders* ("with" term)? "," term` -/
scoped syntax (name := bigexpect) "ğ”¼ " bigOpBinders ("with " term)? ", " term:67 : term

scoped macro_rules (kind := bigexpect)
  | `(ğ”¼ $bs:bigOpBinders $[with $p?]?, $v) => do
    let processed â† processBigOpBinders bs
    let i â† bigOpBindersPattern processed
    let s â† bigOpBindersProd processed
    match p? with
    | some p => `(Finset.expect (Finset.filter (fun $i â†¦ $p) $s) (fun $i â†¦ $v))
    | none => `(Finset.expect $s (fun $i â†¦ $v))

open Lean Meta Parser.Term PrettyPrinter.Delaborator SubExpr
open Batteries.ExtendedBinder

/-- Delaborator for `Finset.expect`. The `pp.funBinderTypes` option controls whether
to show the domain type when the expect is over `Finset.univ`. -/
@[scoped app_delab Finset.expect] def delabFinsetExpect : Delab :=
  whenPPOption getPPNotation <| withOverApp 6 <| do
  let #[_, _, _, _, s, f] := (â† getExpr).getAppArgs | failure
  guard <| f.isLambda
  let ppDomain â† getPPOption getPPFunBinderTypes
  let (i, body) â† withAppArg <| withBindingBodyUnusedName fun i => do
    return (i, â† delab)
  if s.isAppOfArity ``Finset.univ 2 then
    let binder â†
      if ppDomain then
        let ty â† withNaryArg 0 delab
        `(bigOpBinder| $(.mk i):ident : $ty)
      else
        `(bigOpBinder| $(.mk i):ident)
    `(ğ”¼ $binder:bigOpBinder, $body)
  else
    let ss â† withNaryArg 4 <| delab
    `(ğ”¼ $(.mk i):ident âˆˆ $ss, $body)

end BigOperators

open scoped BigOperators

namespace Finset
section AddCommMonoid
variable [AddCommMonoid M] [Module â„šâ‰¥0 M] [AddCommMonoid N] [Module â„šâ‰¥0 N] {s t : Finset Î¹}
  {f g : Î¹ â†’ M} {p q : Î¹ â†’ Prop} [DecidablePred p] [DecidablePred q]

lemma expect_univ [Fintype Î¹] : ğ”¼ i, f i = (âˆ‘ i, f i) /â„š Fintype.card Î¹ := by
  rw [expect, card_univ]

@[simp] lemma expect_empty (f : Î¹ â†’ M) : ğ”¼ i âˆˆ âˆ…, f i = 0 := by simp [expect]
@[simp] lemma expect_singleton (f : Î¹ â†’ M) (i : Î¹) : ğ”¼ j âˆˆ {i}, f j = f i := by simp [expect]
@[simp] lemma expect_const_zero (s : Finset Î¹) : ğ”¼ _i âˆˆ s, (0 : M) = 0 := by simp [expect]

@[congr]
lemma expect_congr {t : Finset Î¹} (hst : s = t) (h : âˆ€ i âˆˆ t, f i = g i) :
    ğ”¼ i âˆˆ s, f i = ğ”¼ i âˆˆ t, g i := by rw [expect, expect, sum_congr hst h, hst]

lemma expectWith_congr (hst : s = t) (hpq : âˆ€ i âˆˆ t, p i â†” q i) (h : âˆ€ i âˆˆ t, q i â†’ f i = g i) :
    ğ”¼ i âˆˆ s with p i, f i = ğ”¼ i âˆˆ t with q i, g i :=
  expect_congr (by rw [hst, filter_inj'.2 hpq]) <| by simpa using h

lemma expect_sum_comm (s : Finset Î¹) (t : Finset Îº) (f : Î¹ â†’ Îº â†’ M) :
    ğ”¼ i âˆˆ s, âˆ‘ j âˆˆ t, f i j = âˆ‘ j âˆˆ t, ğ”¼ i âˆˆ s, f i j := by
  simpa only [expect, smul_sum] using sum_comm

lemma expect_comm (s : Finset Î¹) (t : Finset Îº) (f : Î¹ â†’ Îº â†’ M) :
    ğ”¼ i âˆˆ s, ğ”¼ j âˆˆ t, f i j = ğ”¼ j âˆˆ t, ğ”¼ i âˆˆ s, f i j := by
  rw [expect, expect, â† expect_sum_comm, â† expect_sum_comm, expect, expect, smul_comm, sum_comm]

lemma expect_eq_zero (h : âˆ€ i âˆˆ s, f i = 0) : ğ”¼ i âˆˆ s, f i = 0 :=
  (expect_congr rfl h).trans s.expect_const_zero

lemma exists_ne_zero_of_expect_ne_zero (h : ğ”¼ i âˆˆ s, f i â‰  0) : âˆƒ i âˆˆ s, f i â‰  0 := by
  contrapose! h; exact expect_eq_zero h

lemma expect_add_distrib (s : Finset Î¹) (f g : Î¹ â†’ M) :
    ğ”¼ i âˆˆ s, (f i + g i) = ğ”¼ i âˆˆ s, f i + ğ”¼ i âˆˆ s, g i := by
  simp [expect, sum_add_distrib]

lemma expect_add_expect_comm (fâ‚ fâ‚‚ gâ‚ gâ‚‚ : Î¹ â†’ M) :
    ğ”¼ i âˆˆ s, (fâ‚ i + fâ‚‚ i) + ğ”¼ i âˆˆ s, (gâ‚ i + gâ‚‚ i) =
      ğ”¼ i âˆˆ s, (fâ‚ i + gâ‚ i) + ğ”¼ i âˆˆ s, (fâ‚‚ i + gâ‚‚ i) := by
  simp_rw [expect_add_distrib, add_add_add_comm]

lemma expect_eq_single_of_mem (i : Î¹) (hi : i âˆˆ s) (h : âˆ€ j âˆˆ s, j â‰  i â†’ f j = 0) :
    ğ”¼ i âˆˆ s, f i = f i /â„š #s := by rw [expect, sum_eq_single_of_mem _ hi h]

lemma expect_ite_zero (s : Finset Î¹) (p : Î¹ â†’ Prop) [DecidablePred p]
    (h : âˆ€ i âˆˆ s, âˆ€ j âˆˆ s, p i â†’ p j â†’ i = j) (a : M) :
    ğ”¼ i âˆˆ s, ite (p i) a 0 = ite (âˆƒ i âˆˆ s, p i) (a /â„š #s) 0 := by
  split_ifs <;> simp [expect, sum_ite_zero _ _ h, *]

section DecidableEq
variable [DecidableEq Î¹]

lemma expect_ite_mem (s t : Finset Î¹) (f : Î¹ â†’ M) :
    ğ”¼ i âˆˆ s, (if i âˆˆ t then f i else 0) = (#(s âˆ© t) / #s : â„šâ‰¥0) â€¢ ğ”¼ i âˆˆ s âˆ© t, f i := by
  obtain hst | hst := (s âˆ© t).eq_empty_or_nonempty
  Â· simp [expect, hst]
  Â· simp [expect, smul_smul, â† inv_mul_eq_div, hst.card_ne_zero]

@[simp] lemma expect_dite_eq (i : Î¹) (f : âˆ€ j, i = j â†’ M) :
    ğ”¼ j âˆˆ s, (if h : i = j then f j h else 0) = if i âˆˆ s then f i rfl /â„š #s else 0 := by
  split_ifs <;> simp [expect, *]

@[simp] lemma expect_dite_eq' (i : Î¹) (f : âˆ€ j, j = i â†’ M) :
    ğ”¼ j âˆˆ s, (if h : j = i then f j h else 0) = if i âˆˆ s then f i rfl /â„š #s else 0 := by
  split_ifs <;> simp [expect, *]

@[simp] lemma expect_ite_eq (i : Î¹) (f : Î¹ â†’ M) :
    ğ”¼ j âˆˆ s, (if i = j then f j else 0) = if i âˆˆ s then f i /â„š #s else 0 := by
  split_ifs <;> simp [expect, *]

@[simp] lemma expect_ite_eq' (i : Î¹) (f : Î¹ â†’ M) :
    ğ”¼ j âˆˆ s, (if j = i then f j else 0) = if i âˆˆ s then f i /â„š #s else 0 := by
  split_ifs <;> simp [expect, *]

end DecidableEq

section bij
variable {t : Finset Îº} {g : Îº â†’ M}

/-- Reorder an average.

The difference with `Finset.expect_bij'` is that the bijection is specified as a surjective
injection, rather than by an inverse function.

The difference with `Finset.expect_nbij` is that the bijection is allowed to use membership of the
domain of the average, rather than being a non-dependent function. -/
lemma expect_bij (i : âˆ€ a âˆˆ s, Îº) (hi : âˆ€ a ha, i a ha âˆˆ t) (h : âˆ€ a ha, f a = g (i a ha))
    (i_inj : âˆ€ aâ‚ haâ‚ aâ‚‚ haâ‚‚, i aâ‚ haâ‚ = i aâ‚‚ haâ‚‚ â†’ aâ‚ = aâ‚‚)
    (i_surj : âˆ€ b âˆˆ t, âˆƒ a ha, i a ha = b) : ğ”¼ i âˆˆ s, f i = ğ”¼ i âˆˆ t, g i := by
  simp_rw [expect, card_bij i hi i_inj i_surj, sum_bij i hi i_inj i_surj h]

/-- Reorder an average.

The difference with `Finset.expect_bij` is that the bijection is specified with an inverse, rather
than as a surjective injection.

The difference with `Finset.expect_nbij'` is that the bijection and its inverse are allowed to use
membership of the domains of the averages, rather than being non-dependent functions. -/
lemma expect_bij' (i : âˆ€ a âˆˆ s, Îº) (j : âˆ€ a âˆˆ t, Î¹) (hi : âˆ€ a ha, i a ha âˆˆ t)
    (hj : âˆ€ a ha, j a ha âˆˆ s) (left_inv : âˆ€ a ha, j (i a ha) (hi a ha) = a)
    (right_inv : âˆ€ a ha, i (j a ha) (hj a ha) = a) (h : âˆ€ a ha, f a = g (i a ha)) :
    ğ”¼ i âˆˆ s, f i = ğ”¼ i âˆˆ t, g i := by
  simp_rw [expect, card_bij' i j hi hj left_inv right_inv, sum_bij' i j hi hj left_inv right_inv h]

/-- Reorder an average.

The difference with `Finset.expect_nbij'` is that the bijection is specified as a surjective
injection, rather than by an inverse function.

The difference with `Finset.expect_bij` is that the bijection is a non-dependent function, rather
than being allowed to use membership of the domain of the average. -/
lemma expect_nbij (i : Î¹ â†’ Îº) (hi : âˆ€ a âˆˆ s, i a âˆˆ t) (h : âˆ€ a âˆˆ s, f a = g (i a))
    (i_inj : (s : Set Î¹).InjOn i) (i_surj : (s : Set Î¹).SurjOn i t) :
    ğ”¼ i âˆˆ s, f i = ğ”¼ i âˆˆ t, g i := by
  simp_rw [expect, card_nbij i hi i_inj i_surj, sum_nbij i hi i_inj i_surj h]

/-- Reorder an average.

The difference with `Finset.expect_nbij` is that the bijection is specified with an inverse, rather
than as a surjective injection.

The difference with `Finset.expect_bij'` is that the bijection and its inverse are non-dependent
functions, rather than being allowed to use membership of the domains of the averages.

The difference with `Finset.expect_equiv` is that bijectivity is only required to hold on the
domains of the averages, rather than on the entire types. -/
lemma expect_nbij' (i : Î¹ â†’ Îº) (j : Îº â†’ Î¹) (hi : âˆ€ a âˆˆ s, i a âˆˆ t) (hj : âˆ€ a âˆˆ t, j a âˆˆ s)
    (left_inv : âˆ€ a âˆˆ s, j (i a) = a) (right_inv : âˆ€ a âˆˆ t, i (j a) = a)
    (h : âˆ€ a âˆˆ s, f a = g (i a)) : ğ”¼ i âˆˆ s, f i = ğ”¼ i âˆˆ t, g i := by
  simp_rw [expect, card_nbij' i j hi hj left_inv right_inv,
    sum_nbij' i j hi hj left_inv right_inv h]

/-- `Finset.expect_equiv` is a specialization of `Finset.expect_bij` that automatically fills in
most arguments. -/
lemma expect_equiv (e : Î¹ â‰ƒ Îº) (hst : âˆ€ i, i âˆˆ s â†” e i âˆˆ t) (hfg : âˆ€ i âˆˆ s, f i = g (e i)) :
    ğ”¼ i âˆˆ s, f i = ğ”¼ i âˆˆ t, g i := by simp_rw [expect, card_equiv e hst, sum_equiv e hst hfg]

/-- Expectation over a product set equals the expectation of the fiberwise expectations.

For rewriting in the reverse direction, use `Finset.expect_product'`. -/
lemma expect_product (s : Finset Î¹) (t : Finset Îº) (f : Î¹ Ã— Îº â†’ M) :
    ğ”¼ x âˆˆ s Ã—Ë¢ t, f x = ğ”¼ i âˆˆ s, ğ”¼ j âˆˆ t, f (i, j) := by
  simp only [expect, card_product, sum_product, smul_sum, mul_inv, mul_smul, Nat.cast_mul]

/-- Expectation over a product set equals the expectation of the fiberwise expectations.

For rewriting in the reverse direction, use `Finset.expect_product`. -/
lemma expect_product' (s : Finset Î¹) (t : Finset Îº) (f : Î¹ â†’ Îº â†’ M) :
    ğ”¼ i âˆˆ s Ã—Ë¢ t, f i.1 i.2 = ğ”¼ i âˆˆ s, ğ”¼ j âˆˆ t, f i j := by
  simp only [expect, card_product, sum_product', smul_sum, mul_inv, mul_smul, Nat.cast_mul]

@[simp]
lemma expect_image [DecidableEq Î¹] {m : Îº â†’ Î¹} (hm : (t : Set Îº).InjOn m) :
    ğ”¼ i âˆˆ t.image m, f i = ğ”¼ i âˆˆ t, f (m i) := by
  simp_rw [expect, card_image_of_injOn hm, sum_image hm]

end bij

@[simp] lemma expect_inv_index [DecidableEq Î¹] [InvolutiveInv Î¹] (s : Finset Î¹) (f : Î¹ â†’ M) :
    ğ”¼ i âˆˆ sâ»Â¹, f i = ğ”¼ i âˆˆ s, f iâ»Â¹ := expect_image inv_injective.injOn

@[simp] lemma expect_neg_index [DecidableEq Î¹] [InvolutiveNeg Î¹] (s : Finset Î¹) (f : Î¹ â†’ M) :
    ğ”¼ i âˆˆ -s, f i = ğ”¼ i âˆˆ s, f (-i) := expect_image neg_injective.injOn

lemma _root_.map_expect {F : Type*} [FunLike F M N] [LinearMapClass F â„šâ‰¥0 M N]
    (g : F) (f : Î¹ â†’ M) (s : Finset Î¹) :
    g (ğ”¼ i âˆˆ s, f i) = ğ”¼ i âˆˆ s, g (f i) := by simp only [expect, map_smul, map_sum]

@[simp]
lemma card_smul_expect (s : Finset Î¹) (f : Î¹ â†’ M) : #s â€¢ ğ”¼ i âˆˆ s, f i = âˆ‘ i âˆˆ s, f i := by
  obtain rfl | hs := s.eq_empty_or_nonempty
  Â· simp
  Â· rw [expect, â† Nat.cast_smul_eq_nsmul â„šâ‰¥0, smul_inv_smulâ‚€]
    exact mod_cast hs.card_ne_zero

@[simp] lemma _root_.Fintype.card_smul_expect [Fintype Î¹] (f : Î¹ â†’ M) :
    Fintype.card Î¹ â€¢ ğ”¼ i, f i = âˆ‘ i, f i := Finset.card_smul_expect _ _

@[simp] lemma expect_const (hs : s.Nonempty) (a : M) : ğ”¼ _i âˆˆ s, a = a := by
  rw [expect, sum_const, â† Nat.cast_smul_eq_nsmul â„šâ‰¥0, inv_smul_smulâ‚€]
  exact mod_cast hs.card_ne_zero

lemma smul_expect {G : Type*} [DistribSMul G M] [SMulCommClass G â„šâ‰¥0 M] (a : G)
    (s : Finset Î¹) (f : Î¹ â†’ M) : a â€¢ ğ”¼ i âˆˆ s, f i = ğ”¼ i âˆˆ s, a â€¢ f i := by
  simp only [expect, smul_sum, smul_comm]

end AddCommMonoid

section AddCommGroup
variable [AddCommGroup M] [Module â„šâ‰¥0 M]

lemma expect_sub_distrib (s : Finset Î¹) (f g : Î¹ â†’ M) :
    ğ”¼ i âˆˆ s, (f i - g i) = ğ”¼ i âˆˆ s, f i - ğ”¼ i âˆˆ s, g i := by
  simp only [expect, sum_sub_distrib, smul_sub]

@[simp]
lemma expect_neg_distrib (s : Finset Î¹) (f : Î¹ â†’ M) : ğ”¼ i âˆˆ s, -f i = -ğ”¼ i âˆˆ s, f i := by
  simp [expect]

end AddCommGroup

section Semiring
variable [Semiring M] [Module â„šâ‰¥0 M]

@[simp] lemma card_mul_expect (s : Finset Î¹) (f : Î¹ â†’ M) :
    #s * ğ”¼ i âˆˆ s, f i = âˆ‘ i âˆˆ s, f i := by rw [â† nsmul_eq_mul, card_smul_expect]

@[simp] lemma _root_.Fintype.card_mul_expect [Fintype Î¹] (f : Î¹ â†’ M) :
    Fintype.card Î¹ * ğ”¼ i, f i = âˆ‘ i, f i := Finset.card_mul_expect _ _

lemma expect_mul [IsScalarTower â„šâ‰¥0 M M] (s : Finset Î¹) (f : Î¹ â†’ M) (a : M) :
    (ğ”¼ i âˆˆ s, f i) * a = ğ”¼ i âˆˆ s, f i * a := by rw [expect, expect, smul_mul_assoc, sum_mul]

lemma mul_expect [SMulCommClass â„šâ‰¥0 M M] (s : Finset Î¹) (f : Î¹ â†’ M) (a : M) :
    a * ğ”¼ i âˆˆ s, f i = ğ”¼ i âˆˆ s, a * f i := by rw [expect, expect, mul_smul_comm, mul_sum]

lemma expect_mul_expect [IsScalarTower â„šâ‰¥0 M M] [SMulCommClass â„šâ‰¥0 M M] (s : Finset Î¹)
    (t : Finset Îº) (f : Î¹ â†’ M) (g : Îº â†’ M) :
    (ğ”¼ i âˆˆ s, f i) * ğ”¼ j âˆˆ t, g j = ğ”¼ i âˆˆ s, ğ”¼ j âˆˆ t, f i * g j := by
  simp_rw [expect_mul, mul_expect]

end Semiring

section CommSemiring
variable [CommSemiring M] [Module â„šâ‰¥0 M] [IsScalarTower â„šâ‰¥0 M M] [SMulCommClass â„šâ‰¥0 M M]

lemma expect_pow (s : Finset Î¹) (f : Î¹ â†’ M) (n : â„•) :
    (ğ”¼ i âˆˆ s, f i) ^ n = ğ”¼ p âˆˆ Fintype.piFinset fun _ : Fin n â†¦ s, âˆ i, f (p i) := by
  classical
  rw [expect, smul_pow, sum_pow', expect, Fintype.card_piFinset_const, inv_pow, Nat.cast_pow]

end CommSemiring

section Semifield
variable [Semifield M] [CharZero M]

lemma expect_boole_mul [Fintype Î¹] [Nonempty Î¹] [DecidableEq Î¹] (f : Î¹ â†’ M) (i : Î¹) :
    ğ”¼ j, ite (i = j) (Fintype.card Î¹ : M) 0 * f j = f i := by
  simp_rw [expect_univ, ite_mul, zero_mul, sum_ite_eq, if_pos (mem_univ _)]
  rw [â† @NNRat.cast_natCast M, â† NNRat.smul_def, inv_smul_smulâ‚€]
  simp [Fintype.card_ne_zero]

lemma expect_boole_mul' [Fintype Î¹] [Nonempty Î¹] [DecidableEq Î¹] (f : Î¹ â†’ M) (i : Î¹) :
    ğ”¼ j, ite (j = i) (Fintype.card Î¹ : M) 0 * f j = f i := by
  simp_rw [@eq_comm _ _ i, expect_boole_mul]

lemma expect_eq_sum_div_card (s : Finset Î¹) (f : Î¹ â†’ M) :
    ğ”¼ i âˆˆ s, f i = (âˆ‘ i âˆˆ s, f i) / #s := by
  rw [expect, NNRat.smul_def, div_eq_inv_mul, NNRat.cast_inv, NNRat.cast_natCast]

lemma _root_.Fintype.expect_eq_sum_div_card [Fintype Î¹] (f : Î¹ â†’ M) :
    ğ”¼ i, f i = (âˆ‘ i, f i) / Fintype.card Î¹ := Finset.expect_eq_sum_div_card _ _

lemma expect_div (s : Finset Î¹) (f : Î¹ â†’ M) (a : M) : (ğ”¼ i âˆˆ s, f i) / a = ğ”¼ i âˆˆ s, f i / a := by
  simp_rw [div_eq_mul_inv, expect_mul]

end Semifield

@[simp] lemma expect_apply {Î± : Type*} {Ï€ : Î± â†’ Type*} [âˆ€ a, CommSemiring (Ï€ a)]
    [âˆ€ a, Module â„šâ‰¥0 (Ï€ a)] (s : Finset Î¹) (f : Î¹ â†’ âˆ€ a, Ï€ a) (a : Î±) :
    (ğ”¼ i âˆˆ s, f i) a = ğ”¼ i âˆˆ s, f i a := by simp [expect]

end Finset

namespace algebraMap
variable [Semifield M] [CharZero M] [Semifield N] [CharZero N] [Algebra M N]

@[simp, norm_cast]
lemma coe_expect (s : Finset Î¹) (f : Î¹ â†’ M) : ğ”¼ i âˆˆ s, f i = ğ”¼ i âˆˆ s, (f i : N) :=
  map_expect (algebraMap _ _) _ _

end algebraMap

namespace Fintype
variable [Fintype Î¹] [Fintype Îº]

section AddCommMonoid
variable [AddCommMonoid M] [Module â„šâ‰¥0 M]

/-- `Fintype.expect_bijective` is a variant of `Finset.expect_bij` that accepts
`Function.Bijective`.

See `Function.Bijective.expect_comp` for a version without `h`. -/
lemma expect_bijective (e : Î¹ â†’ Îº) (he : Bijective e) (f : Î¹ â†’ M) (g : Îº â†’ M)
    (h : âˆ€ i, f i = g (e i)) : ğ”¼ i, f i = ğ”¼ i, g i :=
  expect_nbij e (fun _ _ â†¦ mem_univ _) (fun i _ â†¦ h i) he.injective.injOn <| by
    simpa using he.surjective

/-- `Fintype.expect_equiv` is a specialization of `Finset.expect_bij` that automatically fills in
most arguments.

See `Equiv.expect_comp` for a version without `h`. -/
lemma expect_equiv (e : Î¹ â‰ƒ Îº) (f : Î¹ â†’ M) (g : Îº â†’ M) (h : âˆ€ i, f i = g (e i)) :
    ğ”¼ i, f i = ğ”¼ i, g i := expect_bijective _ e.bijective f g h

lemma expect_const [Nonempty Î¹] (a : M) : ğ”¼ _i : Î¹, a = a := Finset.expect_const univ_nonempty _

lemma expect_ite_zero (p : Î¹ â†’ Prop) [DecidablePred p] (h : âˆ€ i j, p i â†’ p j â†’ i = j) (a : M) :
    ğ”¼ i, ite (p i) a 0 = ite (âˆƒ i, p i) (a /â„š Fintype.card Î¹) 0 := by
  simp [univ.expect_ite_zero p (by simpa using h)]

variable [DecidableEq Î¹]

@[simp] lemma expect_ite_mem (s : Finset Î¹) (f : Î¹ â†’ M) :
    ğ”¼ i, (if i âˆˆ s then f i else 0) = s.dens â€¢ ğ”¼ i âˆˆ s, f i := by
  simp [Finset.expect_ite_mem, dens]

lemma expect_dite_eq (i : Î¹) (f : âˆ€ j, i = j â†’ M) :
    ğ”¼ j, (if h : i = j then f j h else 0) = f i rfl /â„š card Î¹ := by simp

lemma expect_dite_eq' (i : Î¹) (f : âˆ€ j, j = i â†’ M) :
    ğ”¼ j, (if h : j = i then f j h else 0) = f i rfl /â„š card Î¹ := by simp

lemma expect_ite_eq (i : Î¹) (f : Î¹ â†’ M) :
    ğ”¼ j, (if i = j then f j else 0) = f i /â„š card Î¹ := by simp

lemma expect_ite_eq' (i : Î¹) (f : Î¹ â†’ M) :
    ğ”¼ j, (if j = i then f j else 0) = f i /â„š card Î¹ := by simp

end AddCommMonoid

section Semiring
variable [Semiring M] [Module â„šâ‰¥0 M]

lemma expect_one [Nonempty Î¹] : ğ”¼ _i : Î¹, (1 : M) = 1 := expect_const _

lemma expect_mul_expect [IsScalarTower â„šâ‰¥0 M M] [SMulCommClass â„šâ‰¥0 M M] (f : Î¹ â†’ M)
    (g : Îº â†’ M) : (ğ”¼ i, f i) * ğ”¼ j, g j = ğ”¼ i, ğ”¼ j, f i * g j :=
  Finset.expect_mul_expect ..

end Semiring
end Fintype
