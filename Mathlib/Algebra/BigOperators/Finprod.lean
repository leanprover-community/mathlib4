/-
Copyright (c) 2020 Kexing Ying and Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying, Kevin Buzzard, Yury Kudryashov
-/
import Mathlib.Algebra.BigOperators.Pi
import Mathlib.Algebra.Group.Indicator
import Mathlib.Algebra.Group.Support
import Mathlib.Algebra.NoZeroSMulDivisors.Basic
import Mathlib.Algebra.Notation.FiniteSupport
import Mathlib.Algebra.Order.BigOperators.Group.Finset
import Mathlib.Algebra.Order.Ring.Defs
import Mathlib.Data.Set.Finite.Lattice

/-!
# Finite products and sums over types and sets

We define products and sums over types and subsets of types, with no finiteness hypotheses.
All infinite products and sums are defined to be junk values (i.e. one or zero).
This approach is sometimes easier to use than `Finset.sum`,
when issues arise with `Finset` and `Fintype` being data.

## Main definitions

We use the following variables:

* `Œ±`, `Œ≤` - types with no structure;
* `s`, `t` - sets
* `M`, `N` - additive or multiplicative commutative monoids
* `f`, `g` - functions

Definitions in this file:

* `finsum f : M` : the sum of `f x` as `x` ranges over the support of `f`, if it's finite.
  Zero otherwise.

* `finprod f : M` : the product of `f x` as `x` ranges over the multiplicative support of `f`, if
  it's finite. One otherwise.

## Notation

* `‚àë·∂† i, f i` and `‚àë·∂† i : Œ±, f i` for `finsum f`

* `‚àè·∂† i, f i` and `‚àè·∂† i : Œ±, f i` for `finprod f`

This notation works for functions `f : p ‚Üí M`, where `p : Prop`, so the following works:

* `‚àë·∂† i ‚àà s, f i`, where `f : Œ± ‚Üí M`, `s : Set Œ±` : sum over the set `s`;
* `‚àë·∂† n < 5, f n`, where `f : ‚Ñï ‚Üí M` : same as `f 0 + f 1 + f 2 + f 3 + f 4`;
* `‚àè·∂† (n >= -2) (hn : n < 3), f n`, where `f : ‚Ñ§ ‚Üí M` : same as `f (-2) * f (-1) * f 0 * f 1 * f 2`.

## Implementation notes

`finsum` and `finprod` is "yet another way of doing finite sums and products in Lean". However
experiments in the wild (e.g. with matroids) indicate that it is a helpful approach in settings
where the user is not interested in computability and wants to do reasoning without running into
typeclass diamonds caused by the constructive finiteness used in definitions such as `Finset` and
`Fintype`. By sticking solely to `Set.Finite` we avoid these problems. We are aware that there are
other solutions but for beginner mathematicians this approach is easier in practice.

Another application is the construction of a partition of unity from a collection of ‚Äúbump‚Äù
functions. In this case the finite set depends on the point and it's convenient to have a definition
that does not mention the set explicitly.

The first arguments in all definitions and lemmas is the codomain of the function of the big
operator. This is necessary for the heuristic in `@[to_additive]`.
See the documentation of `to_additive.attr` for more information.

We did not add `IsFinite (X : Type) : Prop`, because it is simply `Nonempty (Fintype X)`.

## Tags

finsum, finprod, finite sum, finite product
-/


open Function Set

/-!
### Definition and relation to `Finset.sum` and `Finset.prod`
-/

section sort

variable {G M N : Type*} {Œ± Œ≤ Œπ : Sort*} [CommMonoid M] [CommMonoid N]

section

/- Note: we use classical logic only for these definitions, to ensure that we do not write lemmas
with `Classical.dec` in their statement. -/

open Classical in
/-- Sum of `f x` as `x` ranges over the elements of the support of `f`, if it's finite. Zero
otherwise. -/
noncomputable irreducible_def finsum (lemma := finsum_def') [AddCommMonoid M] (f : Œ± ‚Üí M) : M :=
  if h : (support (f ‚àò PLift.down)).Finite then ‚àë i ‚àà h.toFinset, f i.down else 0

open Classical in
/-- Product of `f x` as `x` ranges over the elements of the multiplicative support of `f`, if it's
finite. One otherwise. -/
@[to_additive existing]
noncomputable irreducible_def finprod (lemma := finprod_def') (f : Œ± ‚Üí M) : M :=
  if h : (mulSupport (f ‚àò PLift.down)).Finite then ‚àè i ‚àà h.toFinset, f i.down else 1

attribute [to_additive existing] finprod_def'

end

open Batteries.ExtendedBinder

/-- `‚àë·∂† x, f x` is notation for `finsum f`. It is the sum of `f x`, where `x` ranges over the
support of `f`, if it's finite, zero otherwise. Taking the sum over multiple arguments or
conditions is possible, e.g. `‚àè·∂† (x) (y), f x y` and `‚àè·∂† (x) (h: x ‚àà s), f x` -/
notation3"‚àë·∂† " (...) ", " r:67:(scoped f => finsum f) => r

/-- `‚àè·∂† x, f x` is notation for `finprod f`. It is the product of `f x`, where `x` ranges over the
multiplicative support of `f`, if it's finite, one otherwise. Taking the product over multiple
arguments or conditions is possible, e.g. `‚àè·∂† (x) (y), f x y` and `‚àè·∂† (x) (h: x ‚àà s), f x` -/
notation3"‚àè·∂† " (...) ", " r:67:(scoped f => finprod f) => r

-- Porting note: The following ports the lean3 notation for this file, but is currently very fickle.

-- syntax (name := bigfinsum) "‚àë·∂†" extBinders ", " term:67 : term
-- macro_rules (kind := bigfinsum)
--   | `(‚àë·∂† $x:ident, $p) => `(finsum (fun $x:ident ‚Ü¶ $p))
--   | `(‚àë·∂† $x:ident : $t, $p) => `(finsum (fun $x:ident : $t ‚Ü¶ $p))
--   | `(‚àë·∂† $x:ident $b:binderPred, $p) =>
--     `(finsum fun $x => (finsum (Œ± := satisfies_binder_pred% $x $b) (fun _ => $p)))

--   | `(‚àë·∂† ($x:ident) ($h:ident : $t), $p) =>
--       `(finsum fun ($x) => finsum (Œ± := $t) (fun $h => $p))
--   | `(‚àë·∂† ($x:ident : $_) ($h:ident : $t), $p) =>
--       `(finsum fun ($x) => finsum (Œ± := $t) (fun $h => $p))

--   | `(‚àë·∂† ($x:ident) ($y:ident), $p) =>
--       `(finsum fun $x => (finsum fun $y => $p))
--   | `(‚àë·∂† ($x:ident) ($y:ident) ($h:ident : $t), $p) =>
--       `(finsum fun $x => (finsum fun $y => (finsum (Œ± := $t) fun $h => $p)))

--   | `(‚àë·∂† ($x:ident) ($y:ident) ($z:ident), $p) =>
--       `(finsum fun $x => (finsum fun $y => (finsum fun $z => $p)))
--   | `(‚àë·∂† ($x:ident) ($y:ident) ($z:ident) ($h:ident : $t), $p) =>
--       `(finsum fun $x => (finsum fun $y => (finsum fun $z => (finsum (Œ± := $t) fun $h => $p))))
--
--
-- syntax (name := bigfinprod) "‚àè·∂† " extBinders ", " term:67 : term
-- macro_rules (kind := bigfinprod)
--   | `(‚àè·∂† $x:ident, $p) => `(finprod (fun $x:ident ‚Ü¶ $p))
--   | `(‚àè·∂† $x:ident : $t, $p) => `(finprod (fun $x:ident : $t ‚Ü¶ $p))
--   | `(‚àè·∂† $x:ident $b:binderPred, $p) =>
--     `(finprod fun $x => (finprod (Œ± := satisfies_binder_pred% $x $b) (fun _ => $p)))

--   | `(‚àè·∂† ($x:ident) ($h:ident : $t), $p) =>
--       `(finprod fun ($x) => finprod (Œ± := $t) (fun $h => $p))
--   | `(‚àè·∂† ($x:ident : $_) ($h:ident : $t), $p) =>
--       `(finprod fun ($x) => finprod (Œ± := $t) (fun $h => $p))

--   | `(‚àè·∂† ($x:ident) ($y:ident), $p) =>
--       `(finprod fun $x => (finprod fun $y => $p))
--   | `(‚àè·∂† ($x:ident) ($y:ident) ($h:ident : $t), $p) =>
--       `(finprod fun $x => (finprod fun $y => (finprod (Œ± := $t) fun $h => $p)))

--   | `(‚àè·∂† ($x:ident) ($y:ident) ($z:ident), $p) =>
--       `(finprod fun $x => (finprod fun $y => (finprod fun $z => $p)))
--   | `(‚àè·∂† ($x:ident) ($y:ident) ($z:ident) ($h:ident : $t), $p) =>
--       `(finprod fun $x => (finprod fun $y => (finprod fun $z =>
--          (finprod (Œ± := $t) fun $h => $p))))

@[to_additive]
theorem finprod_eq_prod_plift_of_mulSupport_toFinset_subset {f : Œ± ‚Üí M}
    (hf : (mulSupport (f ‚àò PLift.down)).Finite) {s : Finset (PLift Œ±)} (hs : hf.toFinset ‚äÜ s) :
    ‚àè·∂† i, f i = ‚àè i ‚àà s, f i.down := by
  rw [finprod, dif_pos hf]
  refine Finset.prod_subset hs fun x _ hxf => ?_
  rwa [hf.mem_toFinset, notMem_mulSupport] at hxf

@[to_additive]
theorem finprod_eq_prod_plift_of_mulSupport_subset {f : Œ± ‚Üí M} {s : Finset (PLift Œ±)}
    (hs : mulSupport (f ‚àò PLift.down) ‚äÜ s) : ‚àè·∂† i, f i = ‚àè i ‚àà s, f i.down :=
  finprod_eq_prod_plift_of_mulSupport_toFinset_subset (s.finite_toSet.subset hs) fun x hx => by
    rw [Finite.mem_toFinset] at hx
    exact hs hx

@[to_additive (attr := simp)]
theorem finprod_one : (‚àè·∂† _ : Œ±, (1 : M)) = 1 := by
  have : (mulSupport fun x : PLift Œ± => (fun _ => 1 : Œ± ‚Üí M) x.down) ‚äÜ (‚àÖ : Finset (PLift Œ±)) :=
    fun x h => by simp at h
  rw [finprod_eq_prod_plift_of_mulSupport_subset this, Finset.prod_empty]

@[to_additive]
theorem finprod_of_isEmpty [IsEmpty Œ±] (f : Œ± ‚Üí M) : ‚àè·∂† i, f i = 1 := by
  rw [‚Üê finprod_one]
  congr
  simp [eq_iff_true_of_subsingleton]

@[to_additive (attr := simp)]
theorem finprod_false (f : False ‚Üí M) : ‚àè·∂† i, f i = 1 :=
  finprod_of_isEmpty _

@[to_additive]
theorem finprod_eq_single (f : Œ± ‚Üí M) (a : Œ±) (ha : ‚àÄ x, x ‚â† a ‚Üí f x = 1) :
    ‚àè·∂† x, f x = f a := by
  have : mulSupport (f ‚àò PLift.down) ‚äÜ ({PLift.up a} : Finset (PLift Œ±)) := by
    intro x
    contrapose
    simpa [PLift.eq_up_iff_down_eq] using ha x.down
  rw [finprod_eq_prod_plift_of_mulSupport_subset this, Finset.prod_singleton]

@[to_additive]
theorem finprod_unique [Unique Œ±] (f : Œ± ‚Üí M) : ‚àè·∂† i, f i = f default :=
  finprod_eq_single f default fun _x hx => (hx <| Unique.eq_default _).elim

@[to_additive (attr := simp)]
theorem finprod_true (f : True ‚Üí M) : ‚àè·∂† i, f i = f trivial :=
  @finprod_unique M True _ ‚ü®‚ü®trivial‚ü©, fun _ => rfl‚ü© f

@[to_additive]
theorem finprod_eq_dif {p : Prop} [Decidable p] (f : p ‚Üí M) :
    ‚àè·∂† i, f i = if h : p then f h else 1 := by
  split_ifs with h
  ¬∑ haveI : Unique p := ‚ü®‚ü®h‚ü©, fun _ => rfl‚ü©
    exact finprod_unique f
  ¬∑ haveI : IsEmpty p := ‚ü®h‚ü©
    exact finprod_of_isEmpty f

@[to_additive]
theorem finprod_eq_if {p : Prop} [Decidable p] {x : M} : ‚àè·∂† _ : p, x = if p then x else 1 :=
  finprod_eq_dif fun _ => x

@[to_additive]
theorem finprod_congr {f g : Œ± ‚Üí M} (h : ‚àÄ x, f x = g x) : finprod f = finprod g :=
  congr_arg _ <| funext h

@[to_additive (attr := congr)]
theorem finprod_congr_Prop {p q : Prop} {f : p ‚Üí M} {g : q ‚Üí M} (hpq : p = q)
    (hfg : ‚àÄ h : q, f (hpq.mpr h) = g h) : finprod f = finprod g := by
  subst q
  exact finprod_congr hfg

/-- To prove a property of a finite product, it suffices to prove that the property is
multiplicative and holds on the factors. -/
@[to_additive
      /-- To prove a property of a finite sum, it suffices to prove that the property is
      additive and holds on the summands. -/]
theorem finprod_induction {f : Œ± ‚Üí M} (p : M ‚Üí Prop) (hp‚ÇÄ : p 1)
    (hp‚ÇÅ : ‚àÄ x y, p x ‚Üí p y ‚Üí p (x * y)) (hp‚ÇÇ : ‚àÄ i, p (f i)) : p (‚àè·∂† i, f i) := by
  rw [finprod]
  split_ifs
  exacts [Finset.prod_induction _ _ hp‚ÇÅ hp‚ÇÄ fun i _ => hp‚ÇÇ _, hp‚ÇÄ]

theorem finprod_nonneg {R : Type*} [CommSemiring R] [PartialOrder R] [IsOrderedRing R]
    {f : Œ± ‚Üí R} (hf : ‚àÄ x, 0 ‚â§ f x) :
    0 ‚â§ ‚àè·∂† x, f x :=
  finprod_induction (fun x => 0 ‚â§ x) zero_le_one (fun _ _ => mul_nonneg) hf

@[to_additive finsum_nonneg]
theorem one_le_finprod' {M : Type*} [CommMonoid M] [PartialOrder M] [IsOrderedMonoid M]
    {f : Œ± ‚Üí M} (hf : ‚àÄ i, 1 ‚â§ f i) :
    1 ‚â§ ‚àè·∂† i, f i :=
  finprod_induction _ le_rfl (fun _ _ => one_le_mul) hf

@[to_additive]
theorem MonoidHom.map_finprod_plift (f : M ‚Üí* N) (g : Œ± ‚Üí M)
    (h : (mulSupport <| g ‚àò PLift.down).Finite) : f (‚àè·∂† x, g x) = ‚àè·∂† x, f (g x) := by
  rw [finprod_eq_prod_plift_of_mulSupport_subset h.coe_toFinset.ge,
    finprod_eq_prod_plift_of_mulSupport_subset, map_prod]
  rw [h.coe_toFinset]
  exact mulSupport_comp_subset f.map_one (g ‚àò PLift.down)

@[to_additive]
theorem MonoidHom.map_finprod_Prop {p : Prop} (f : M ‚Üí* N) (g : p ‚Üí M) :
    f (‚àè·∂† x, g x) = ‚àè·∂† x, f (g x) :=
  f.map_finprod_plift g (Set.toFinite _)

@[to_additive]
theorem MonoidHom.map_finprod_of_preimage_one (f : M ‚Üí* N) (hf : ‚àÄ x, f x = 1 ‚Üí x = 1) (g : Œ± ‚Üí M) :
    f (‚àè·∂† i, g i) = ‚àè·∂† i, f (g i) := by
  by_cases hg : (mulSupport <| g ‚àò PLift.down).Finite; ¬∑ exact f.map_finprod_plift g hg
  rw [finprod, dif_neg, f.map_one, finprod, dif_neg]
  exacts [Infinite.mono (fun x hx => mt (hf (g x.down)) hx) hg, hg]

@[to_additive]
theorem MonoidHom.map_finprod_of_injective (g : M ‚Üí* N) (hg : Injective g) (f : Œ± ‚Üí M) :
    g (‚àè·∂† i, f i) = ‚àè·∂† i, g (f i) :=
  g.map_finprod_of_preimage_one (fun _ => (hg.eq_iff' g.map_one).mp) f

@[to_additive]
theorem MulEquiv.map_finprod (g : M ‚âÉ* N) (f : Œ± ‚Üí M) : g (‚àè·∂† i, f i) = ‚àè·∂† i, g (f i) :=
  g.toMonoidHom.map_finprod_of_injective (EquivLike.injective g) f

@[to_additive]
theorem MulEquivClass.map_finprod {F : Type*} [EquivLike F M N] [MulEquivClass F M N] (g : F)
    (f : Œ± ‚Üí M) : g (‚àè·∂† i, f i) = ‚àè·∂† i, g (f i) :=
  MulEquiv.map_finprod (MulEquivClass.toMulEquiv g) f

/-- The `NoZeroSMulDivisors` makes sure that the result holds even when the support of `f` is
infinite. For a more usual version assuming `(support f).Finite` instead, see `finsum_smul'`. -/
theorem finsum_smul {R M : Type*} [Ring R] [AddCommGroup M] [Module R M] [NoZeroSMulDivisors R M]
    (f : Œπ ‚Üí R) (x : M) : (‚àë·∂† i, f i) ‚Ä¢ x = ‚àë·∂† i, f i ‚Ä¢ x := by
  rcases eq_or_ne x 0 with (rfl | hx)
  ¬∑ simp
  ¬∑ exact ((smulAddHom R M).flip x).map_finsum_of_injective (smul_left_injective R hx) _

/-- The `NoZeroSMulDivisors` makes sure that the result holds even when the support of `f` is
infinite. For a more usual version assuming `(support f).Finite` instead, see `smul_finsum'`. -/
theorem smul_finsum {R M : Type*} [Semiring R] [AddCommGroup M] [Module R M]
    [NoZeroSMulDivisors R M] (c : R) (f : Œπ ‚Üí M) : (c ‚Ä¢ ‚àë·∂† i, f i) = ‚àë·∂† i, c ‚Ä¢ f i := by
  rcases eq_or_ne c 0 with (rfl | hc)
  ¬∑ simp
  ¬∑ exact (smulAddHom R M c).map_finsum_of_injective (smul_right_injective M hc) _

@[to_additive]
theorem finprod_inv_distrib [DivisionCommMonoid G] (f : Œ± ‚Üí G) : (‚àè·∂† x, (f x)‚Åª¬π) = (‚àè·∂† x, f x)‚Åª¬π :=
  ((MulEquiv.inv G).map_finprod f).symm

end sort

section type

variable {Œ± Œ≤ Œπ G M N : Type*} [CommMonoid M] [CommMonoid N]

@[to_additive]
theorem finprod_eq_mulIndicator_apply (s : Set Œ±) (f : Œ± ‚Üí M) (a : Œ±) :
    ‚àè·∂† _ : a ‚àà s, f a = mulIndicator s f a := by
  classical convert finprod_eq_if (M := M) (p := a ‚àà s) (x := f a)

@[to_additive (attr := simp)]
theorem finprod_apply_ne_one (f : Œ± ‚Üí M) (a : Œ±) : ‚àè·∂† _ : f a ‚â† 1, f a = f a := by
  rw [‚Üê mem_mulSupport, finprod_eq_mulIndicator_apply, mulIndicator_mulSupport]

@[to_additive]
theorem finprod_mem_def (s : Set Œ±) (f : Œ± ‚Üí M) : ‚àè·∂† a ‚àà s, f a = ‚àè·∂† a, mulIndicator s f a :=
  finprod_congr <| finprod_eq_mulIndicator_apply s f

@[to_additive]
lemma finprod_mem_mulSupport (f : Œ± ‚Üí M) : ‚àè·∂† a ‚àà mulSupport f, f a = ‚àè·∂† a, f a := by
  rw [finprod_mem_def, mulIndicator_mulSupport]

@[to_additive]
theorem finprod_eq_prod_of_mulSupport_subset (f : Œ± ‚Üí M) {s : Finset Œ±} (h : mulSupport f ‚äÜ s) :
    ‚àè·∂† i, f i = ‚àè i ‚àà s, f i := by
  have A : mulSupport (f ‚àò PLift.down) = Equiv.plift.symm '' mulSupport f := by
    rw [mulSupport_comp_eq_preimage]
    exact (Equiv.plift.symm.image_eq_preimage _).symm
  have : mulSupport (f ‚àò PLift.down) ‚äÜ s.map Equiv.plift.symm.toEmbedding := by
    rw [A, Finset.coe_map]
    exact image_mono h
  rw [finprod_eq_prod_plift_of_mulSupport_subset this]
  simp only [Finset.prod_map, Equiv.coe_toEmbedding]
  congr

@[to_additive]
theorem finprod_eq_prod_of_mulSupport_toFinset_subset (f : Œ± ‚Üí M) (hf : (mulSupport f).Finite)
    {s : Finset Œ±} (h : hf.toFinset ‚äÜ s) : ‚àè·∂† i, f i = ‚àè i ‚àà s, f i :=
  finprod_eq_prod_of_mulSupport_subset _ fun _ hx => h <| hf.mem_toFinset.2 hx

@[to_additive]
theorem finprod_eq_finset_prod_of_mulSupport_subset (f : Œ± ‚Üí M) {s : Finset Œ±}
    (h : mulSupport f ‚äÜ (s : Set Œ±)) : ‚àè·∂† i, f i = ‚àè i ‚àà s, f i :=
  haveI h' : (s.finite_toSet.subset h).toFinset ‚äÜ s := by
    simpa [‚Üê Finset.coe_subset, Set.coe_toFinset]
  finprod_eq_prod_of_mulSupport_toFinset_subset _ _ h'

@[to_additive]
theorem finprod_def (f : Œ± ‚Üí M) [Decidable (mulSupport f).Finite] :
    ‚àè·∂† i : Œ±, f i = if h : (mulSupport f).Finite then ‚àè i ‚àà h.toFinset, f i else 1 := by
  split_ifs with h
  ¬∑ exact finprod_eq_prod_of_mulSupport_toFinset_subset _ h (Finset.Subset.refl _)
  ¬∑ rw [finprod, dif_neg]
    rw [mulSupport_comp_eq_preimage]
    exact mt (fun hf => hf.of_preimage Equiv.plift.surjective) h

@[to_additive]
theorem finprod_of_infinite_mulSupport {f : Œ± ‚Üí M} (hf : (mulSupport f).Infinite) :
    ‚àè·∂† i, f i = 1 := by classical rw [finprod_def, dif_neg hf]

@[to_additive]
theorem finprod_eq_prod (f : Œ± ‚Üí M) (hf : (mulSupport f).Finite) :
    ‚àè·∂† i : Œ±, f i = ‚àè i ‚àà hf.toFinset, f i := by classical rw [finprod_def, dif_pos hf]

@[to_additive]
theorem finprod_eq_prod_of_fintype [Fintype Œ±] (f : Œ± ‚Üí M) : ‚àè·∂† i : Œ±, f i = ‚àè i, f i :=
  finprod_eq_prod_of_mulSupport_toFinset_subset _ (Set.toFinite _) <| Finset.subset_univ _

@[to_additive]
theorem map_finset_prod {Œ± F : Type*} [Fintype Œ±] [EquivLike F M N] [MulEquivClass F M N] (f : F)
    (g : Œ± ‚Üí M) : f (‚àè i : Œ±, g i) = ‚àè i : Œ±, f (g i) := by
  simp [‚Üê finprod_eq_prod_of_fintype, MulEquivClass.map_finprod]

@[to_additive]
theorem finprod_cond_eq_prod_of_cond_iff (f : Œ± ‚Üí M) {p : Œ± ‚Üí Prop} {t : Finset Œ±}
    (h : ‚àÄ {x}, f x ‚â† 1 ‚Üí (p x ‚Üî x ‚àà t)) : (‚àè·∂† (i) (_ : p i), f i) = ‚àè i ‚àà t, f i := by
  set s := { x | p x }
  change ‚àè·∂† (i : Œ±) (_ : i ‚àà s), f i = ‚àè i ‚àà t, f i
  have : mulSupport (s.mulIndicator f) ‚äÜ t := by
    rw [Set.mulSupport_mulIndicator]
    intro x hx
    exact (h hx.2).1 hx.1
  rw [finprod_mem_def, finprod_eq_prod_of_mulSupport_subset _ this]
  refine Finset.prod_congr rfl fun x hx => mulIndicator_apply_eq_self.2 fun hxs => ?_
  contrapose! hxs
  exact (h hxs).2 hx

@[to_additive]
theorem finprod_cond_ne (f : Œ± ‚Üí M) (a : Œ±) [DecidableEq Œ±] (hf : (mulSupport f).Finite) :
    (‚àè·∂† (i) (_ : i ‚â† a), f i) = ‚àè i ‚àà hf.toFinset.erase a, f i := by
  apply finprod_cond_eq_prod_of_cond_iff
  intro x hx
  rw [Finset.mem_erase, Finite.mem_toFinset, mem_mulSupport]
  grind

@[to_additive]
theorem finprod_mem_eq_prod_of_inter_mulSupport_eq (f : Œ± ‚Üí M) {s : Set Œ±} {t : Finset Œ±}
    (h : s ‚à© mulSupport f = t.toSet ‚à© mulSupport f) : ‚àè·∂† i ‚àà s, f i = ‚àè i ‚àà t, f i :=
  finprod_cond_eq_prod_of_cond_iff _ <| by
    intro x hxf
    rw [‚Üê mem_mulSupport] at hxf
    refine ‚ü®fun hx => ?_, fun hx => ?_‚ü©
    ¬∑ refine ((mem_inter_iff x t (mulSupport f)).mp ?_).1
      rw [‚Üê Set.ext_iff.mp h x, mem_inter_iff]
      exact ‚ü®hx, hxf‚ü©
    ¬∑ refine ((mem_inter_iff x s (mulSupport f)).mp ?_).1
      rw [Set.ext_iff.mp h x, mem_inter_iff]
      exact ‚ü®hx, hxf‚ü©

@[to_additive]
theorem finprod_mem_eq_prod_of_subset (f : Œ± ‚Üí M) {s : Set Œ±} {t : Finset Œ±}
    (h‚ÇÅ : s ‚à© mulSupport f ‚äÜ t) (h‚ÇÇ : ‚Üët ‚äÜ s) : ‚àè·∂† i ‚àà s, f i = ‚àè i ‚àà t, f i :=
  finprod_cond_eq_prod_of_cond_iff _ fun hx => ‚ü®fun h => h‚ÇÅ ‚ü®h, hx‚ü©, fun h => h‚ÇÇ h‚ü©

@[to_additive]
theorem finprod_mem_eq_prod (f : Œ± ‚Üí M) {s : Set Œ±} (hf : (s ‚à© mulSupport f).Finite) :
    ‚àè·∂† i ‚àà s, f i = ‚àè i ‚àà hf.toFinset, f i :=
  finprod_mem_eq_prod_of_inter_mulSupport_eq _ <| by simp [inter_assoc]

@[to_additive]
theorem finprod_mem_eq_prod_filter (f : Œ± ‚Üí M) (s : Set Œ±) [DecidablePred (¬∑ ‚àà s)]
    (hf : (mulSupport f).Finite) :
    ‚àè·∂† i ‚àà s, f i = ‚àè i ‚àà hf.toFinset with i ‚àà s, f i :=
  finprod_mem_eq_prod_of_inter_mulSupport_eq _ <| by
    ext x
    simp [and_comm]

@[to_additive]
theorem finprod_mem_eq_toFinset_prod (f : Œ± ‚Üí M) (s : Set Œ±) [Fintype s] :
    ‚àè·∂† i ‚àà s, f i = ‚àè i ‚àà s.toFinset, f i :=
  finprod_mem_eq_prod_of_inter_mulSupport_eq _ <| by simp_rw [coe_toFinset s]

@[to_additive]
theorem finprod_mem_eq_finite_toFinset_prod (f : Œ± ‚Üí M) {s : Set Œ±} (hs : s.Finite) :
    ‚àè·∂† i ‚àà s, f i = ‚àè i ‚àà hs.toFinset, f i :=
  finprod_mem_eq_prod_of_inter_mulSupport_eq _ <| by rw [hs.coe_toFinset]

@[to_additive]
theorem finprod_mem_finset_eq_prod (f : Œ± ‚Üí M) (s : Finset Œ±) : ‚àè·∂† i ‚àà s, f i = ‚àè i ‚àà s, f i :=
  finprod_mem_eq_prod_of_inter_mulSupport_eq _ rfl

@[to_additive]
theorem finprod_mem_coe_finset (f : Œ± ‚Üí M) (s : Finset Œ±) :
    (‚àè·∂† i ‚àà (s : Set Œ±), f i) = ‚àè i ‚àà s, f i :=
  finprod_mem_eq_prod_of_inter_mulSupport_eq _ rfl

@[to_additive]
theorem finprod_mem_eq_one_of_infinite {f : Œ± ‚Üí M} {s : Set Œ±} (hs : (s ‚à© mulSupport f).Infinite) :
    ‚àè·∂† i ‚àà s, f i = 1 := by
  rw [finprod_mem_def]
  apply finprod_of_infinite_mulSupport
  rwa [‚Üê mulSupport_mulIndicator] at hs

@[to_additive]
theorem finprod_mem_eq_one_of_forall_eq_one {f : Œ± ‚Üí M} {s : Set Œ±} (h : ‚àÄ x ‚àà s, f x = 1) :
    ‚àè·∂† i ‚àà s, f i = 1 := by simp +contextual [h]

@[to_additive]
theorem finprod_mem_inter_mulSupport (f : Œ± ‚Üí M) (s : Set Œ±) :
    ‚àè·∂† i ‚àà s ‚à© mulSupport f, f i = ‚àè·∂† i ‚àà s, f i := by
  rw [finprod_mem_def, finprod_mem_def, mulIndicator_inter_mulSupport]

@[to_additive]
theorem finprod_mem_inter_mulSupport_eq (f : Œ± ‚Üí M) (s t : Set Œ±)
    (h : s ‚à© mulSupport f = t ‚à© mulSupport f) : ‚àè·∂† i ‚àà s, f i = ‚àè·∂† i ‚àà t, f i := by
  rw [‚Üê finprod_mem_inter_mulSupport, h, finprod_mem_inter_mulSupport]

@[to_additive]
theorem finprod_mem_inter_mulSupport_eq' (f : Œ± ‚Üí M) (s t : Set Œ±)
    (h : ‚àÄ x ‚àà mulSupport f, x ‚àà s ‚Üî x ‚àà t) : ‚àè·∂† i ‚àà s, f i = ‚àè·∂† i ‚àà t, f i := by
  apply finprod_mem_inter_mulSupport_eq
  ext x
  exact and_congr_left (h x)

@[to_additive]
theorem finprod_mem_univ (f : Œ± ‚Üí M) : ‚àè·∂† i ‚àà @Set.univ Œ±, f i = ‚àè·∂† i : Œ±, f i :=
  finprod_congr fun _ => finprod_true _

variable {f g : Œ± ‚Üí M} {a b : Œ±} {s t : Set Œ±}

@[to_additive]
theorem finprod_mem_congr (h‚ÇÄ : s = t) (h‚ÇÅ : ‚àÄ x ‚àà t, f x = g x) :
    ‚àè·∂† i ‚àà s, f i = ‚àè·∂† i ‚àà t, g i :=
  h‚ÇÄ.symm ‚ñ∏ finprod_congr fun i => finprod_congr_Prop rfl (h‚ÇÅ i)

@[to_additive]
theorem finprod_eq_one_of_forall_eq_one {f : Œ± ‚Üí M} (h : ‚àÄ x, f x = 1) : ‚àè·∂† i, f i = 1 := by
  simp +contextual [h]

@[to_additive finsum_pos']
theorem one_lt_finprod' {M : Type*} [CommMonoid M] [PartialOrder M] [IsOrderedCancelMonoid M]
    {f : Œπ ‚Üí M}
    (h : ‚àÄ i, 1 ‚â§ f i) (h' : ‚àÉ i, 1 < f i) (hf : (mulSupport f).Finite) : 1 < ‚àè·∂† i, f i := by
  rcases h' with ‚ü®i, hi‚ü©
  rw [finprod_eq_prod _ hf]
  refine Finset.one_lt_prod' (fun i _ ‚Ü¶ h i) ‚ü®i, ?_, hi‚ü©
  simpa only [Finite.mem_toFinset, mem_mulSupport] using ne_of_gt hi

/-!
### Distributivity w.r.t. addition, subtraction, and (scalar) multiplication
-/


/-- If the multiplicative supports of `f` and `g` are finite, then the product of `f i * g i` equals
the product of `f i` multiplied by the product of `g i`. -/
@[to_additive
      /-- If the additive supports of `f` and `g` are finite, then the sum of `f i + g i`
      equals the sum of `f i` plus the sum of `g i`. -/]
theorem finprod_mul_distrib (hf : (mulSupport f).Finite) (hg : (mulSupport g).Finite) :
    ‚àè·∂† i, f i * g i = (‚àè·∂† i, f i) * ‚àè·∂† i, g i := by
  classical
    rw [finprod_eq_prod_of_mulSupport_toFinset_subset f hf Finset.subset_union_left,
      finprod_eq_prod_of_mulSupport_toFinset_subset g hg Finset.subset_union_right, ‚Üê
      Finset.prod_mul_distrib]
    refine finprod_eq_prod_of_mulSupport_subset _ ?_
    simp only [Finset.coe_union, Finite.coe_toFinset, mulSupport_subset_iff,
      mem_union, mem_mulSupport]
    intro x
    contrapose!
    rintro ‚ü®hf, hg‚ü©
    simp [hf, hg]

/-- If the multiplicative supports of `f` and `g` are finite, then the product of `f i / g i`
equals the product of `f i` divided by the product of `g i`. -/
@[to_additive
      /-- If the additive supports of `f` and `g` are finite, then the sum of `f i - g i`
      equals the sum of `f i` minus the sum of `g i`. -/]
theorem finprod_div_distrib [DivisionCommMonoid G] {f g : Œ± ‚Üí G} (hf : (mulSupport f).Finite)
    (hg : (mulSupport g).Finite) : ‚àè·∂† i, f i / g i = (‚àè·∂† i, f i) / ‚àè·∂† i, g i := by
  simp only [div_eq_mul_inv, finprod_mul_distrib hf ((mulSupport_fun_inv g).symm.rec hg),
    finprod_inv_distrib]

/-- A more general version of `finprod_mem_mul_distrib` that only requires `s ‚à© mulSupport f` and
`s ‚à© mulSupport g` rather than `s` to be finite. -/
@[to_additive
      /-- A more general version of `finsum_mem_add_distrib` that only requires `s ‚à© support f`
      and `s ‚à© support g` rather than `s` to be finite. -/]
theorem finprod_mem_mul_distrib' (hf : (s ‚à© mulSupport f).Finite) (hg : (s ‚à© mulSupport g).Finite) :
    ‚àè·∂† i ‚àà s, f i * g i = (‚àè·∂† i ‚àà s, f i) * ‚àè·∂† i ‚àà s, g i := by
  rw [‚Üê mulSupport_mulIndicator] at hf hg
  simp only [finprod_mem_def, mulIndicator_mul, finprod_mul_distrib hf hg]

/-- The product of the constant function `1` over any set equals `1`. -/
@[to_additive /-- The sum of the constant function `0` over any set equals `0`. -/]
theorem finprod_mem_one (s : Set Œ±) : (‚àè·∂† i ‚àà s, (1 : M)) = 1 := by simp

/-- If a function `f` equals `1` on a set `s`, then the product of `f i` over `i ‚àà s` equals `1`. -/
@[to_additive
      /-- If a function `f` equals `0` on a set `s`, then the sum of `f i` over `i ‚àà s`
      equals `0`. -/]
theorem finprod_mem_of_eqOn_one (hf : s.EqOn f 1) : ‚àè·∂† i ‚àà s, f i = 1 := by
  rw [‚Üê finprod_mem_one s]
  exact finprod_mem_congr rfl hf

/-- If the product of `f i` over `i ‚àà s` is not equal to `1`, then there is some `x ‚àà s` such that
`f x ‚â† 1`. -/
@[to_additive
      /-- If the sum of `f i` over `i ‚àà s` is not equal to `0`, then there is some `x ‚àà s`
      such that `f x ‚â† 0`. -/]
theorem exists_ne_one_of_finprod_mem_ne_one (h : ‚àè·∂† i ‚àà s, f i ‚â† 1) : ‚àÉ x ‚àà s, f x ‚â† 1 := by
  by_contra! h'
  exact h (finprod_mem_of_eqOn_one h')

/-- Given a finite set `s`, the product of `f i * g i` over `i ‚àà s` equals the product of `f i`
over `i ‚àà s` times the product of `g i` over `i ‚àà s`. -/
@[to_additive
      /-- Given a finite set `s`, the sum of `f i + g i` over `i ‚àà s` equals the sum of `f i`
      over `i ‚àà s` plus the sum of `g i` over `i ‚àà s`. -/]
theorem finprod_mem_mul_distrib (hs : s.Finite) :
    ‚àè·∂† i ‚àà s, f i * g i = (‚àè·∂† i ‚àà s, f i) * ‚àè·∂† i ‚àà s, g i :=
  finprod_mem_mul_distrib' (hs.inter_of_left _) (hs.inter_of_left _)

@[to_additive]
theorem MonoidHom.map_finprod {f : Œ± ‚Üí M} (g : M ‚Üí* N) (hf : (mulSupport f).Finite) :
    g (‚àè·∂† i, f i) = ‚àè·∂† i, g (f i) :=
  g.map_finprod_plift f <| hf.preimage Equiv.plift.injective.injOn

@[to_additive]
theorem finprod_pow (hf : (mulSupport f).Finite) (n : ‚Ñï) : (‚àè·∂† i, f i) ^ n = ‚àè·∂† i, f i ^ n :=
  (powMonoidHom n).map_finprod hf

/-- See also `finsum_smul` for a version that works even when the support of `f` is not finite,
but with slightly stronger typeclass requirements. -/
theorem finsum_smul' {R M : Type*} [Semiring R] [AddCommMonoid M] [Module R M] {f : Œπ ‚Üí R}
    (hf : (support f).Finite) (x : M) : (‚àë·∂† i, f i) ‚Ä¢ x = ‚àë·∂† i, f i ‚Ä¢ x :=
  ((smulAddHom R M).flip x).map_finsum hf

/-- See also `smul_finsum` for a version that works even when the support of `f` is not finite,
but with slightly stronger typeclass requirements. -/
theorem smul_finsum' {R M : Type*} [Monoid R] [AddCommMonoid M] [DistribMulAction R M] (c : R)
    {f : Œπ ‚Üí M} (hf : (support f).Finite) : (c ‚Ä¢ ‚àë·∂† i, f i) = ‚àë·∂† i, c ‚Ä¢ f i :=
  (DistribMulAction.toAddMonoidHom M c).map_finsum hf

/-- A more general version of `MonoidHom.map_finprod_mem` that requires `s ‚à© mulSupport f` rather
than `s` to be finite. -/
@[to_additive
      /-- A more general version of `AddMonoidHom.map_finsum_mem` that requires
      `s ‚à© support f` rather than `s` to be finite. -/]
theorem MonoidHom.map_finprod_mem' {f : Œ± ‚Üí M} (g : M ‚Üí* N) (h‚ÇÄ : (s ‚à© mulSupport f).Finite) :
    g (‚àè·∂† j ‚àà s, f j) = ‚àè·∂† i ‚àà s, g (f i) := by
  rw [g.map_finprod]
  ¬∑ simp only [g.map_finprod_Prop]
  ¬∑ simpa only [finprod_eq_mulIndicator_apply, mulSupport_mulIndicator]

/-- Given a monoid homomorphism `g : M ‚Üí* N` and a function `f : Œ± ‚Üí M`, the value of `g` at the
product of `f i` over `i ‚àà s` equals the product of `g (f i)` over `s`. -/
@[to_additive
      /-- Given an additive monoid homomorphism `g : M ‚Üí* N` and a function `f : Œ± ‚Üí M`, the
      value of `g` at the sum of `f i` over `i ‚àà s` equals the sum of `g (f i)` over `s`. -/]
theorem MonoidHom.map_finprod_mem (f : Œ± ‚Üí M) (g : M ‚Üí* N) (hs : s.Finite) :
    g (‚àè·∂† j ‚àà s, f j) = ‚àè·∂† i ‚àà s, g (f i) :=
  g.map_finprod_mem' (hs.inter_of_left _)

@[to_additive]
theorem MulEquiv.map_finprod_mem (g : M ‚âÉ* N) (f : Œ± ‚Üí M) {s : Set Œ±} (hs : s.Finite) :
    g (‚àè·∂† i ‚àà s, f i) = ‚àè·∂† i ‚àà s, g (f i) :=
  g.toMonoidHom.map_finprod_mem f hs

@[to_additive]
theorem finprod_mem_inv_distrib [DivisionCommMonoid G] (f : Œ± ‚Üí G) (hs : s.Finite) :
    (‚àè·∂† x ‚àà s, (f x)‚Åª¬π) = (‚àè·∂† x ‚àà s, f x)‚Åª¬π :=
  ((MulEquiv.inv G).map_finprod_mem f hs).symm

/-- Given a finite set `s`, the product of `f i / g i` over `i ‚àà s` equals the product of `f i`
over `i ‚àà s` divided by the product of `g i` over `i ‚àà s`. -/
@[to_additive
      /-- Given a finite set `s`, the sum of `f i / g i` over `i ‚àà s` equals the sum of `f i`
      over `i ‚àà s` minus the sum of `g i` over `i ‚àà s`. -/]
theorem finprod_mem_div_distrib [DivisionCommMonoid G] (f g : Œ± ‚Üí G) (hs : s.Finite) :
    ‚àè·∂† i ‚àà s, f i / g i = (‚àè·∂† i ‚àà s, f i) / ‚àè·∂† i ‚àà s, g i := by
  simp only [div_eq_mul_inv, finprod_mem_mul_distrib hs, finprod_mem_inv_distrib g hs]

/-!
### `‚àè·∂† x ‚àà s, f x` and set operations
-/


/-- The product of any function over an empty set is `1`. -/
@[to_additive /-- The sum of any function over an empty set is `0`. -/]
theorem finprod_mem_empty : (‚àè·∂† i ‚àà (‚àÖ : Set Œ±), f i) = 1 := by simp

/-- A set `s` is nonempty if the product of some function over `s` is not equal to `1`. -/
@[to_additive
/-- A set `s` is nonempty if the sum of some function over `s` is not equal to `0`. -/]
theorem nonempty_of_finprod_mem_ne_one (h : ‚àè·∂† i ‚àà s, f i ‚â† 1) : s.Nonempty :=
  nonempty_iff_ne_empty.2 fun h' => h <| h'.symm ‚ñ∏ finprod_mem_empty

/-- Given finite sets `s` and `t`, the product of `f i` over `i ‚àà s ‚à™ t` times the product of
`f i` over `i ‚àà s ‚à© t` equals the product of `f i` over `i ‚àà s` times the product of `f i`
over `i ‚àà t`. -/
@[to_additive
      /-- Given finite sets `s` and `t`, the sum of `f i` over `i ‚àà s ‚à™ t` plus the sum of
      `f i` over `i ‚àà s ‚à© t` equals the sum of `f i` over `i ‚àà s` plus the sum of `f i`
      over `i ‚àà t`. -/]
theorem finprod_mem_union_inter (hs : s.Finite) (ht : t.Finite) :
    ((‚àè·∂† i ‚àà s ‚à™ t, f i) * ‚àè·∂† i ‚àà s ‚à© t, f i) = (‚àè·∂† i ‚àà s, f i) * ‚àè·∂† i ‚àà t, f i := by
  lift s to Finset Œ± using hs; lift t to Finset Œ± using ht
  classical
    rw [‚Üê Finset.coe_union, ‚Üê Finset.coe_inter]
    simp only [finprod_mem_coe_finset, Finset.prod_union_inter]

/-- A more general version of `finprod_mem_union_inter` that requires `s ‚à© mulSupport f` and
`t ‚à© mulSupport f` rather than `s` and `t` to be finite. -/
@[to_additive
      /-- A more general version of `finsum_mem_union_inter` that requires `s ‚à© support f` and
      `t ‚à© support f` rather than `s` and `t` to be finite. -/]
theorem finprod_mem_union_inter' (hs : (s ‚à© mulSupport f).Finite) (ht : (t ‚à© mulSupport f).Finite) :
    ((‚àè·∂† i ‚àà s ‚à™ t, f i) * ‚àè·∂† i ‚àà s ‚à© t, f i) = (‚àè·∂† i ‚àà s, f i) * ‚àè·∂† i ‚àà t, f i := by
  rw [‚Üê finprod_mem_inter_mulSupport f s, ‚Üê finprod_mem_inter_mulSupport f t, ‚Üê
    finprod_mem_union_inter hs ht, ‚Üê union_inter_distrib_right, finprod_mem_inter_mulSupport, ‚Üê
    finprod_mem_inter_mulSupport f (s ‚à© t)]
  congr 2
  rw [inter_left_comm, inter_assoc, inter_assoc, inter_self, inter_left_comm]

/-- A more general version of `finprod_mem_union` that requires `s ‚à© mulSupport f` and
`t ‚à© mulSupport f` rather than `s` and `t` to be finite. -/
@[to_additive
      /-- A more general version of `finsum_mem_union` that requires `s ‚à© support f` and
      `t ‚à© support f` rather than `s` and `t` to be finite. -/]
theorem finprod_mem_union' (hst : Disjoint s t) (hs : (s ‚à© mulSupport f).Finite)
    (ht : (t ‚à© mulSupport f).Finite) : ‚àè·∂† i ‚àà s ‚à™ t, f i = (‚àè·∂† i ‚àà s, f i) * ‚àè·∂† i ‚àà t, f i := by
  rw [‚Üê finprod_mem_union_inter' hs ht, disjoint_iff_inter_eq_empty.1 hst, finprod_mem_empty,
    mul_one]

/-- Given two finite disjoint sets `s` and `t`, the product of `f i` over `i ‚àà s ‚à™ t` equals the
product of `f i` over `i ‚àà s` times the product of `f i` over `i ‚àà t`. -/
@[to_additive
      /-- Given two finite disjoint sets `s` and `t`, the sum of `f i` over `i ‚àà s ‚à™ t` equals
      the sum of `f i` over `i ‚àà s` plus the sum of `f i` over `i ‚àà t`. -/]
theorem finprod_mem_union (hst : Disjoint s t) (hs : s.Finite) (ht : t.Finite) :
    ‚àè·∂† i ‚àà s ‚à™ t, f i = (‚àè·∂† i ‚àà s, f i) * ‚àè·∂† i ‚àà t, f i :=
  finprod_mem_union' hst (hs.inter_of_left _) (ht.inter_of_left _)

/-- A more general version of `finprod_mem_union'` that requires `s ‚à© mulSupport f` and
`t ‚à© mulSupport f` rather than `s` and `t` to be disjoint -/
@[to_additive
      /-- A more general version of `finsum_mem_union'` that requires `s ‚à© support f` and
      `t ‚à© support f` rather than `s` and `t` to be disjoint -/]
theorem finprod_mem_union'' (hst : Disjoint (s ‚à© mulSupport f) (t ‚à© mulSupport f))
    (hs : (s ‚à© mulSupport f).Finite) (ht : (t ‚à© mulSupport f).Finite) :
    ‚àè·∂† i ‚àà s ‚à™ t, f i = (‚àè·∂† i ‚àà s, f i) * ‚àè·∂† i ‚àà t, f i := by
  rw [‚Üê finprod_mem_inter_mulSupport f s, ‚Üê finprod_mem_inter_mulSupport f t, ‚Üê
    finprod_mem_union hst hs ht, ‚Üê union_inter_distrib_right, finprod_mem_inter_mulSupport]

/-- The product of `f i` over `i ‚àà {a}` equals `f a`. -/
@[to_additive /-- The sum of `f i` over `i ‚àà {a}` equals `f a`. -/]
theorem finprod_mem_singleton : (‚àè·∂† i ‚àà ({a} : Set Œ±), f i) = f a := by
  rw [‚Üê Finset.coe_singleton, finprod_mem_coe_finset, Finset.prod_singleton]

@[to_additive (attr := simp)]
theorem finprod_cond_eq_left : (‚àè·∂† (i) (_ : i = a), f i) = f a :=
  finprod_mem_singleton

@[to_additive (attr := simp)]
theorem finprod_cond_eq_right : (‚àè·∂† (i) (_ : a = i), f i) = f a := by simp [@eq_comm _ a]

/-- A more general version of `finprod_mem_insert` that requires `s ‚à© mulSupport f` rather than `s`
to be finite. -/
@[to_additive
      /-- A more general version of `finsum_mem_insert` that requires `s ‚à© support f` rather
      than `s` to be finite. -/]
theorem finprod_mem_insert' (f : Œ± ‚Üí M) (h : a ‚àâ s) (hs : (s ‚à© mulSupport f).Finite) :
    ‚àè·∂† i ‚àà insert a s, f i = f a * ‚àè·∂† i ‚àà s, f i := by
  rw [insert_eq, finprod_mem_union' _ _ hs, finprod_mem_singleton]
  ¬∑ rwa [disjoint_singleton_left]
  ¬∑ exact (finite_singleton a).inter_of_left _

/-- Given a finite set `s` and an element `a ‚àâ s`, the product of `f i` over `i ‚àà insert a s` equals
`f a` times the product of `f i` over `i ‚àà s`. -/
@[to_additive
      /-- Given a finite set `s` and an element `a ‚àâ s`, the sum of `f i` over `i ‚àà insert a s`
      equals `f a` plus the sum of `f i` over `i ‚àà s`. -/]
theorem finprod_mem_insert (f : Œ± ‚Üí M) (h : a ‚àâ s) (hs : s.Finite) :
    ‚àè·∂† i ‚àà insert a s, f i = f a * ‚àè·∂† i ‚àà s, f i :=
  finprod_mem_insert' f h <| hs.inter_of_left _

/-- If `f a = 1` when `a ‚àâ s`, then the product of `f i` over `i ‚àà insert a s` equals the product of
`f i` over `i ‚àà s`. -/
@[to_additive
      /-- If `f a = 0` when `a ‚àâ s`, then the sum of `f i` over `i ‚àà insert a s` equals the sum
      of `f i` over `i ‚àà s`. -/]
theorem finprod_mem_insert_of_eq_one_if_notMem (h : a ‚àâ s ‚Üí f a = 1) :
    ‚àè·∂† i ‚àà insert a s, f i = ‚àè·∂† i ‚àà s, f i := by
  refine finprod_mem_inter_mulSupport_eq' _ _ _ fun x hx => ‚ü®?_, Or.inr‚ü©
  rintro (rfl | hxs)
  exacts [not_imp_comm.1 h hx, hxs]

@[deprecated (since := "2025-05-23")]
alias finsum_mem_insert_of_eq_zero_if_not_mem := finsum_mem_insert_of_eq_zero_if_notMem

@[to_additive existing, deprecated (since := "2025-05-23")]
alias finprod_mem_insert_of_eq_one_if_not_mem := finprod_mem_insert_of_eq_one_if_notMem

/-- If `f a = 1`, then the product of `f i` over `i ‚àà insert a s` equals the product of `f i` over
`i ‚àà s`. -/
@[to_additive
      /-- If `f a = 0`, then the sum of `f i` over `i ‚àà insert a s` equals the sum of `f i`
      over `i ‚àà s`. -/]
theorem finprod_mem_insert_one (h : f a = 1) : ‚àè·∂† i ‚àà insert a s, f i = ‚àè·∂† i ‚àà s, f i :=
  finprod_mem_insert_of_eq_one_if_notMem fun _ => h

/-- If the multiplicative support of `f` is finite, then for every `x` in the domain of `f`, `f x`
divides `finprod f`. -/
theorem finprod_mem_dvd {f : Œ± ‚Üí N} (a : Œ±) (hf : (mulSupport f).Finite) : f a ‚à£ finprod f := by
  by_cases ha : a ‚àà mulSupport f
  ¬∑ rw [finprod_eq_prod_of_mulSupport_toFinset_subset f hf (Set.Subset.refl _)]
    exact Finset.dvd_prod_of_mem f ((Finite.mem_toFinset hf).mpr ha)
  ¬∑ rw [notMem_mulSupport.mp ha]
    exact one_dvd (finprod f)

/-- The product of `f i` over `i ‚àà {a, b}`, `a ‚â† b`, is equal to `f a * f b`. -/
@[to_additive /-- The sum of `f i` over `i ‚àà {a, b}`, `a ‚â† b`, is equal to `f a + f b`. -/]
theorem finprod_mem_pair (h : a ‚â† b) : (‚àè·∂† i ‚àà ({a, b} : Set Œ±), f i) = f a * f b := by
  rw [finprod_mem_insert, finprod_mem_singleton]
  exacts [h, finite_singleton b]

/-- The product of `f y` over `y ‚àà g '' s` equals the product of `f (g i)` over `s`
provided that `g` is injective on `s ‚à© mulSupport (f ‚àò g)`. -/
@[to_additive
      /-- The sum of `f y` over `y ‚àà g '' s` equals the sum of `f (g i)` over `s` provided that
      `g` is injective on `s ‚à© support (f ‚àò g)`. -/]
theorem finprod_mem_image' {s : Set Œ≤} {g : Œ≤ ‚Üí Œ±} (hg : (s ‚à© mulSupport (f ‚àò g)).InjOn g) :
    ‚àè·∂† i ‚àà g '' s, f i = ‚àè·∂† j ‚àà s, f (g j) := by
  classical
    by_cases hs : (s ‚à© mulSupport (f ‚àò g)).Finite
    ¬∑ have hg : ‚àÄ x ‚àà hs.toFinset, ‚àÄ y ‚àà hs.toFinset, g x = g y ‚Üí x = y := by
        simpa only [hs.mem_toFinset]
      have := finprod_mem_eq_prod (comp f g) hs
      unfold Function.comp at this
      rw [this, ‚Üê Finset.prod_image hg]
      refine finprod_mem_eq_prod_of_inter_mulSupport_eq f ?_
      rw [Finset.coe_image, hs.coe_toFinset, ‚Üê image_inter_mulSupport_eq, inter_assoc, inter_self]
    ¬∑ unfold Function.comp at hs
      rw [finprod_mem_eq_one_of_infinite hs, finprod_mem_eq_one_of_infinite]
      rwa [image_inter_mulSupport_eq, infinite_image_iff hg]

/-- The product of `f y` over `y ‚àà g '' s` equals the product of `f (g i)` over `s` provided that
`g` is injective on `s`. -/
@[to_additive
      /-- The sum of `f y` over `y ‚àà g '' s` equals the sum of `f (g i)` over `s` provided that
      `g` is injective on `s`. -/]
theorem finprod_mem_image {s : Set Œ≤} {g : Œ≤ ‚Üí Œ±} (hg : s.InjOn g) :
    ‚àè·∂† i ‚àà g '' s, f i = ‚àè·∂† j ‚àà s, f (g j) :=
  finprod_mem_image' <| hg.mono inter_subset_left

/-- The product of `f y` over `y ‚àà Set.range g` equals the product of `f (g i)` over all `i`
provided that `g` is injective on `mulSupport (f ‚àò g)`. -/
@[to_additive
      /-- The sum of `f y` over `y ‚àà Set.range g` equals the sum of `f (g i)` over all `i`
      provided that `g` is injective on `support (f ‚àò g)`. -/]
theorem finprod_mem_range' {g : Œ≤ ‚Üí Œ±} (hg : (mulSupport (f ‚àò g)).InjOn g) :
    ‚àè·∂† i ‚àà range g, f i = ‚àè·∂† j, f (g j) := by
  rw [‚Üê image_univ, finprod_mem_image', finprod_mem_univ]
  rwa [univ_inter]

/-- The product of `f y` over `y ‚àà Set.range g` equals the product of `f (g i)` over all `i`
provided that `g` is injective. -/
@[to_additive
      /-- The sum of `f y` over `y ‚àà Set.range g` equals the sum of `f (g i)` over all `i`
      provided that `g` is injective. -/]
theorem finprod_mem_range {g : Œ≤ ‚Üí Œ±} (hg : Injective g) : ‚àè·∂† i ‚àà range g, f i = ‚àè·∂† j, f (g j) :=
  finprod_mem_range' hg.injOn

/-- See also `Finset.prod_bij`. -/
@[to_additive /-- See also `Finset.sum_bij`. -/]
theorem finprod_mem_eq_of_bijOn {s : Set Œ±} {t : Set Œ≤} {f : Œ± ‚Üí M} {g : Œ≤ ‚Üí M} (e : Œ± ‚Üí Œ≤)
    (he‚ÇÄ : s.BijOn e t) (he‚ÇÅ : ‚àÄ x ‚àà s, f x = g (e x)) : ‚àè·∂† i ‚àà s, f i = ‚àè·∂† j ‚àà t, g j := by
  rw [‚Üê Set.BijOn.image_eq he‚ÇÄ, finprod_mem_image he‚ÇÄ.2.1]
  exact finprod_mem_congr rfl he‚ÇÅ

/-- See `finprod_comp`, `Fintype.prod_bijective` and `Finset.prod_bij`. -/
@[to_additive /-- See `finsum_comp`, `Fintype.sum_bijective` and `Finset.sum_bij`. -/]
theorem finprod_eq_of_bijective {f : Œ± ‚Üí M} {g : Œ≤ ‚Üí M} (e : Œ± ‚Üí Œ≤) (he‚ÇÄ : Bijective e)
    (he‚ÇÅ : ‚àÄ x, f x = g (e x)) : ‚àè·∂† i, f i = ‚àè·∂† j, g j := by
  rw [‚Üê finprod_mem_univ f, ‚Üê finprod_mem_univ g]
  exact finprod_mem_eq_of_bijOn _ (bijective_iff_bijOn_univ.mp he‚ÇÄ) fun x _ => he‚ÇÅ x

/-- See also `finprod_eq_of_bijective`, `Fintype.prod_bijective` and `Finset.prod_bij`. -/
@[to_additive
/-- See also `finsum_eq_of_bijective`, `Fintype.sum_bijective` and `Finset.sum_bij`. -/]
theorem finprod_comp {g : Œ≤ ‚Üí M} (e : Œ± ‚Üí Œ≤) (he‚ÇÄ : Function.Bijective e) :
    (‚àè·∂† i, g (e i)) = ‚àè·∂† j, g j :=
  finprod_eq_of_bijective e he‚ÇÄ fun _ => rfl

@[to_additive]
theorem finprod_comp_equiv (e : Œ± ‚âÉ Œ≤) {f : Œ≤ ‚Üí M} : (‚àè·∂† i, f (e i)) = ‚àè·∂† i', f i' :=
  finprod_comp e e.bijective

@[to_additive]
theorem finprod_set_coe_eq_finprod_mem (s : Set Œ±) : ‚àè·∂† j : s, f j = ‚àè·∂† i ‚àà s, f i := by
  rw [‚Üê finprod_mem_range, Subtype.range_coe]
  exact Subtype.coe_injective

@[to_additive]
theorem finprod_subtype_eq_finprod_cond (p : Œ± ‚Üí Prop) :
    ‚àè·∂† j : Subtype p, f j = ‚àè·∂† (i) (_ : p i), f i :=
  finprod_set_coe_eq_finprod_mem { i | p i }

@[to_additive]
theorem finprod_mem_inter_mul_diff' (t : Set Œ±) (h : (s ‚à© mulSupport f).Finite) :
    ((‚àè·∂† i ‚àà s ‚à© t, f i) * ‚àè·∂† i ‚àà s \ t, f i) = ‚àè·∂† i ‚àà s, f i := by
  rw [‚Üê finprod_mem_union', inter_union_diff]
  ¬∑ rw [disjoint_iff_inf_le]
    exact fun x hx => hx.2.2 hx.1.2
  exacts [h.subset fun x hx => ‚ü®hx.1.1, hx.2‚ü©, h.subset fun x hx => ‚ü®hx.1.1, hx.2‚ü©]

@[to_additive]
theorem finprod_mem_inter_mul_diff (t : Set Œ±) (h : s.Finite) :
    ((‚àè·∂† i ‚àà s ‚à© t, f i) * ‚àè·∂† i ‚àà s \ t, f i) = ‚àè·∂† i ‚àà s, f i :=
  finprod_mem_inter_mul_diff' _ <| h.inter_of_left _

/-- A more general version of `finprod_mem_mul_diff` that requires `t ‚à© mulSupport f` rather than
`t` to be finite. -/
@[to_additive
      /-- A more general version of `finsum_mem_add_diff` that requires `t ‚à© support f` rather
      than `t` to be finite. -/]
theorem finprod_mem_mul_diff' (hst : s ‚äÜ t) (ht : (t ‚à© mulSupport f).Finite) :
    ((‚àè·∂† i ‚àà s, f i) * ‚àè·∂† i ‚àà t \ s, f i) = ‚àè·∂† i ‚àà t, f i := by
  rw [‚Üê finprod_mem_inter_mul_diff' _ ht, inter_eq_self_of_subset_right hst]

/-- Given a finite set `t` and a subset `s` of `t`, the product of `f i` over `i ‚àà s`
times the product of `f i` over `t \ s` equals the product of `f i` over `i ‚àà t`. -/
@[to_additive
      /-- Given a finite set `t` and a subset `s` of `t`, the sum of `f i` over `i ‚àà s` plus
      the sum of `f i` over `t \ s` equals the sum of `f i` over `i ‚àà t`. -/]
theorem finprod_mem_mul_diff (hst : s ‚äÜ t) (ht : t.Finite) :
    ((‚àè·∂† i ‚àà s, f i) * ‚àè·∂† i ‚àà t \ s, f i) = ‚àè·∂† i ‚àà t, f i :=
  finprod_mem_mul_diff' hst (ht.inter_of_left _)

/-- Given a family of pairwise disjoint finite sets `t i` indexed by a finite type, the product of
`f a` over the union `‚ãÉ i, t i` is equal to the product over all indexes `i` of the products of
`f a` over `a ‚àà t i`. -/
@[to_additive
      /-- Given a family of pairwise disjoint finite sets `t i` indexed by a finite type, the
      sum of `f a` over the union `‚ãÉ i, t i` is equal to the sum over all indexes `i` of the
      sums of `f a` over `a ‚àà t i`. -/]
theorem finprod_mem_iUnion [Finite Œπ] {t : Œπ ‚Üí Set Œ±} (h : Pairwise (Disjoint on t))
    (ht : ‚àÄ i, (t i).Finite) : ‚àè·∂† a ‚àà ‚ãÉ i : Œπ, t i, f a = ‚àè·∂† i, ‚àè·∂† a ‚àà t i, f a := by
  cases nonempty_fintype Œπ
  lift t to Œπ ‚Üí Finset Œ± using ht
  classical
    rw [‚Üê biUnion_univ, ‚Üê Finset.coe_univ, ‚Üê Finset.coe_biUnion, finprod_mem_coe_finset,
      Finset.prod_biUnion]
    ¬∑ simp only [finprod_mem_coe_finset, finprod_eq_prod_of_fintype]
    ¬∑ exact fun x _ y _ hxy => Finset.disjoint_coe.1 (h hxy)

/-- Given a family of sets `t : Œπ ‚Üí Set Œ±`, a finite set `I` in the index type such that all sets
`t i`, `i ‚àà I`, are finite, if all `t i`, `i ‚àà I`, are pairwise disjoint, then the product of `f a`
over `a ‚àà ‚ãÉ i ‚àà I, t i` is equal to the product over `i ‚àà I` of the products of `f a` over
`a ‚àà t i`. -/
@[to_additive
      /-- Given a family of sets `t : Œπ ‚Üí Set Œ±`, a finite set `I` in the index type such that
      all sets `t i`, `i ‚àà I`, are finite, if all `t i`, `i ‚àà I`, are pairwise disjoint, then the
      sum of `f a` over `a ‚àà ‚ãÉ i ‚àà I, t i` is equal to the sum over `i ‚àà I` of the sums of `f a`
      over `a ‚àà t i`. -/]
theorem finprod_mem_biUnion {I : Set Œπ} {t : Œπ ‚Üí Set Œ±} (h : I.PairwiseDisjoint t) (hI : I.Finite)
    (ht : ‚àÄ i ‚àà I, (t i).Finite) : ‚àè·∂† a ‚àà ‚ãÉ x ‚àà I, t x, f a = ‚àè·∂† i ‚àà I, ‚àè·∂† j ‚àà t i, f j := by
  haveI := hI.fintype
  rw [biUnion_eq_iUnion, finprod_mem_iUnion, ‚Üê finprod_set_coe_eq_finprod_mem]
  exacts [fun x y hxy => h x.2 y.2 (Subtype.coe_injective.ne hxy), fun b => ht b b.2]

/-- If `t` is a finite set of pairwise disjoint finite sets, then the product of `f a`
over `a ‚àà ‚ãÉ‚ÇÄ t` is the product over `s ‚àà t` of the products of `f a` over `a ‚àà s`. -/
@[to_additive
      /-- If `t` is a finite set of pairwise disjoint finite sets, then the sum of `f a` over
      `a ‚àà ‚ãÉ‚ÇÄ t` is the sum over `s ‚àà t` of the sums of `f a` over `a ‚àà s`. -/]
theorem finprod_mem_sUnion {t : Set (Set Œ±)} (h : t.PairwiseDisjoint id) (ht‚ÇÄ : t.Finite)
    (ht‚ÇÅ : ‚àÄ x ‚àà t, Set.Finite x) : ‚àè·∂† a ‚àà ‚ãÉ‚ÇÄ t, f a = ‚àè·∂† s ‚àà t, ‚àè·∂† a ‚àà s, f a := by
  rw [Set.sUnion_eq_biUnion]
  exact finprod_mem_biUnion h ht‚ÇÄ ht‚ÇÅ

@[to_additive]
lemma finprod_option {f : Option Œ± ‚Üí M} (hf : (mulSupport (f ‚àò some)).Finite) :
    ‚àè·∂† o, f o = f none * ‚àè·∂† a, f (some a) := by
  replace hf : (mulSupport f).Finite := by simpa [finite_option]
  convert finprod_mem_insert' f (show none ‚àâ Set.range Option.some by simp)
    (hf.subset inter_subset_right)
  ¬∑ simp
  ¬∑ rw [finprod_mem_range]
    exact Option.some_injective _

@[to_additive]
lemma finprod_mem_powerset_insert {f : Set Œ± ‚Üí M} {s : Set Œ±} {a : Œ±} (hs : s.Finite)
    (has : a ‚àâ s) : ‚àè·∂† t ‚àà ùí´ insert a s, f t = (‚àè·∂† t ‚àà ùí´ s, f t) * ‚àè·∂† t ‚àà ùí´ s, f (insert a t) := by
  rw [Set.powerset_insert,
    finprod_mem_union (disjoint_powerset_insert has) hs.powerset (hs.powerset.image (insert a)),
    finprod_mem_image (powerset_insert_injOn has)]

@[to_additive]
lemma finprod_mem_powerset_diff_elem {f : Set Œ± ‚Üí M} {s : Set Œ±} {a : Œ±} (hs : s.Finite)
    (has : a ‚àà s) : ‚àè·∂† t ‚àà ùí´ s, f t = (‚àè·∂† t ‚àà ùí´ (s \ {a}), f t)
    * ‚àè·∂† t ‚àà ùí´ (s \ {a}), f (insert a t) := by
  nth_rw 1 2 [‚Üê Set.insert_diff_self_of_mem has] -- second appearance hidden by notation
  exact finprod_mem_powerset_insert (hs.subset Set.diff_subset)
    (notMem_diff_of_mem (Set.mem_singleton a))

@[to_additive]
theorem mul_finprod_cond_ne (a : Œ±) (hf : (mulSupport f).Finite) :
    (f a * ‚àè·∂† (i) (_ : i ‚â† a), f i) = ‚àè·∂† i, f i := by
  classical
    rw [finprod_eq_prod _ hf]
    have h : ‚àÄ x : Œ±, f x ‚â† 1 ‚Üí (x ‚â† a ‚Üî x ‚àà hf.toFinset \ {a}) := by
      intro x hx
      rw [Finset.mem_sdiff, Finset.mem_singleton, Finite.mem_toFinset, mem_mulSupport]
      grind
    rw [finprod_cond_eq_prod_of_cond_iff f (fun hx => h _ hx), Finset.sdiff_singleton_eq_erase]
    by_cases ha : a ‚àà mulSupport f
    ¬∑ apply Finset.mul_prod_erase _ _ ((Finite.mem_toFinset _).mpr ha)
    ¬∑ rw [mem_mulSupport, not_not] at ha
      rw [ha, one_mul]
      apply Finset.prod_erase _ ha

/-- If `s : Set Œ±` and `t : Set Œ≤` are finite sets, then taking the product over `s` commutes with
taking the product over `t`. -/
@[to_additive
      /-- If `s : Set Œ±` and `t : Set Œ≤` are finite sets, then summing over `s` commutes with
      summing over `t`. -/]
theorem finprod_mem_comm {s : Set Œ±} {t : Set Œ≤} (f : Œ± ‚Üí Œ≤ ‚Üí M) (hs : s.Finite) (ht : t.Finite) :
    (‚àè·∂† i ‚àà s, ‚àè·∂† j ‚àà t, f i j) = ‚àè·∂† j ‚àà t, ‚àè·∂† i ‚àà s, f i j := by
  lift s to Finset Œ± using hs; lift t to Finset Œ≤ using ht
  simp only [finprod_mem_coe_finset]
  exact Finset.prod_comm

/-- To prove a property of a finite product, it suffices to prove that the property is
multiplicative and holds on factors. -/
@[to_additive
      /-- To prove a property of a finite sum, it suffices to prove that the property is
      additive and holds on summands. -/]
theorem finprod_mem_induction (p : M ‚Üí Prop) (hp‚ÇÄ : p 1) (hp‚ÇÅ : ‚àÄ x y, p x ‚Üí p y ‚Üí p (x * y))
    (hp‚ÇÇ : ‚àÄ x ‚àà s, p <| f x) : p (‚àè·∂† i ‚àà s, f i) :=
  finprod_induction _ hp‚ÇÄ hp‚ÇÅ fun x => finprod_induction _ hp‚ÇÄ hp‚ÇÅ <| hp‚ÇÇ x

theorem finprod_cond_nonneg {R : Type*} [CommSemiring R] [PartialOrder R] [IsOrderedRing R]
    {p : Œ± ‚Üí Prop} {f : Œ± ‚Üí R}
    (hf : ‚àÄ x, p x ‚Üí 0 ‚â§ f x) : 0 ‚â§ ‚àè·∂† (x) (_ : p x), f x :=
  finprod_nonneg fun x => finprod_nonneg <| hf x

@[to_additive]
theorem single_le_finprod {M : Type*} [CommMonoid M] [PartialOrder M] [IsOrderedMonoid M]
    (i : Œ±) {f : Œ± ‚Üí M}
    (hf : (mulSupport f).Finite) (h : ‚àÄ j, 1 ‚â§ f j) : f i ‚â§ ‚àè·∂† j, f j := by
  classical calc
      f i ‚â§ ‚àè j ‚àà insert i hf.toFinset, f j :=
        Finset.single_le_prod' (fun j _ => h j) (Finset.mem_insert_self _ _)
      _ = ‚àè·∂† j, f j :=
        (finprod_eq_prod_of_mulSupport_toFinset_subset _ hf (Finset.subset_insert _ _)).symm

theorem finprod_eq_zero {M‚ÇÄ : Type*} [CommMonoidWithZero M‚ÇÄ] (f : Œ± ‚Üí M‚ÇÄ) (x : Œ±) (hx : f x = 0)
    (hf : (mulSupport f).Finite) : ‚àè·∂† x, f x = 0 := by
  nontriviality
  rw [finprod_eq_prod f hf]
  refine Finset.prod_eq_zero (hf.mem_toFinset.2 ?_) hx
  simp [hx]

@[to_additive]
theorem finprod_prod_comm (s : Finset Œ≤) (f : Œ± ‚Üí Œ≤ ‚Üí M)
    (h : ‚àÄ b ‚àà s, (mulSupport fun a => f a b).Finite) :
    (‚àè·∂† a : Œ±, ‚àè b ‚àà s, f a b) = ‚àè b ‚àà s, ‚àè·∂† a : Œ±, f a b := by
  have hU :
    (mulSupport fun a => ‚àè b ‚àà s, f a b) ‚äÜ
      (s.finite_toSet.biUnion fun b hb => h b (Finset.mem_coe.1 hb)).toFinset := by
    rw [Finite.coe_toFinset]
    intro x hx
    simp only [exists_prop, mem_iUnion, Ne, mem_mulSupport, Finset.mem_coe]
    contrapose! hx
    rw [mem_mulSupport, not_not, Finset.prod_congr rfl hx, Finset.prod_const_one]
  rw [finprod_eq_prod_of_mulSupport_subset _ hU, Finset.prod_comm]
  refine Finset.prod_congr rfl fun b hb => (finprod_eq_prod_of_mulSupport_subset _ ?_).symm
  intro a ha
  simp only [Finite.coe_toFinset, mem_iUnion]
  exact ‚ü®b, hb, ha‚ü©

@[to_additive]
theorem prod_finprod_comm (s : Finset Œ±) (f : Œ± ‚Üí Œ≤ ‚Üí M) (h : ‚àÄ a ‚àà s, (mulSupport (f a)).Finite) :
    (‚àè a ‚àà s, ‚àè·∂† b : Œ≤, f a b) = ‚àè·∂† b : Œ≤, ‚àè a ‚àà s, f a b :=
  (finprod_prod_comm s (fun b a => f a b) h).symm

theorem mul_finsum {R : Type*} [NonUnitalNonAssocSemiring R] (f : Œ± ‚Üí R) (r : R)
    (h : (support f).Finite) : (r * ‚àë·∂† a : Œ±, f a) = ‚àë·∂† a : Œ±, r * f a :=
  (AddMonoidHom.mulLeft r).map_finsum h

theorem mul_finsum_mem {R : Type*} [NonUnitalNonAssocSemiring R] {s : Set Œ±} (f : Œ± ‚Üí R) (r : R)
    (hs : s.Finite) : (r * ‚àë·∂† a ‚àà s, f a) = ‚àë·∂† a ‚àà s, r * f a :=
  (AddMonoidHom.mulLeft r).map_finsum_mem f hs

theorem finsum_mul {R : Type*} [NonUnitalNonAssocSemiring R] (f : Œ± ‚Üí R) (r : R)
    (h : (support f).Finite) : (‚àë·∂† a : Œ±, f a) * r = ‚àë·∂† a : Œ±, f a * r :=
  (AddMonoidHom.mulRight r).map_finsum h

theorem finsum_mem_mul {R : Type*} [NonUnitalNonAssocSemiring R] {s : Set Œ±} (f : Œ± ‚Üí R) (r : R)
    (hs : s.Finite) : (‚àë·∂† a ‚àà s, f a) * r = ‚àë·∂† a ‚àà s, f a * r :=
  (AddMonoidHom.mulRight r).map_finsum_mem f hs

@[to_additive (attr := simp)]
lemma finprod_apply {Œ± Œπ : Type*} {f : Œπ ‚Üí Œ± ‚Üí N} (hf : (mulSupport f).Finite) (a : Œ±) :
    (‚àè·∂† i, f i) a = ‚àè·∂† i, f i a := by
  classical
  have hf' : (mulSupport fun i ‚Ü¶ f i a).Finite := hf.subset (by aesop)
  simp only [finprod_def, dif_pos, hf, hf', Finset.prod_apply]
  symm
  apply Finset.prod_subset <;> aesop

@[to_additive]
theorem Finset.mulSupport_of_fiberwise_prod_subset_image [DecidableEq Œ≤] (s : Finset Œ±) (f : Œ± ‚Üí M)
    (g : Œ± ‚Üí Œ≤) : (mulSupport fun b => ‚àè a ‚àà s with g a = b, f a) ‚äÜ s.image g := by
  simp only [Finset.coe_image]
  intro b h
  suffices {a ‚àà s | g a = b}.Nonempty by
    simpa only [fiber_nonempty_iff_mem_image, Finset.mem_image, exists_prop]
  exact Finset.nonempty_of_prod_ne_one h

/-- Note that `b ‚àà (s.filter (fun ab => Prod.fst ab = a)).image Prod.snd` iff `(a, b) ‚àà s` so
we can simplify the right-hand side of this lemma. However the form stated here is more useful for
iterating this lemma, e.g., if we have `f : Œ± √ó Œ≤ √ó Œ≥ ‚Üí M`. -/
@[to_additive
      /-- Note that `b ‚àà (s.filter (fun ab => Prod.fst ab = a)).image Prod.snd` iff `(a, b) ‚àà s` so
      we can simplify the right-hand side of this lemma. However the form stated here is more
      useful for iterating this lemma, e.g., if we have `f : Œ± √ó Œ≤ √ó Œ≥ ‚Üí M`. -/]
theorem finprod_mem_finset_product' [DecidableEq Œ±] [DecidableEq Œ≤] (s : Finset (Œ± √ó Œ≤))
    (f : Œ± √ó Œ≤ ‚Üí M) :
    (‚àè·∂† (ab) (_ : ab ‚àà s), f ab) =
      ‚àè·∂† (a) (b) (_ : b ‚àà (s.filter fun ab => Prod.fst ab = a).image Prod.snd), f (a, b) := by
  have (a : _) :
      ‚àè i ‚àà (s.filter fun ab => Prod.fst ab = a).image Prod.snd, f (a, i) =
        (s.filter (Prod.fst ¬∑ = a)).prod f := by
    refine Finset.prod_nbij' (fun b ‚Ü¶ (a, b)) Prod.snd ?_ ?_ ?_ ?_ ?_ <;> aesop
  rw [finprod_mem_finset_eq_prod]
  simp_rw [finprod_mem_finset_eq_prod, this]
  rw [finprod_eq_prod_of_mulSupport_subset _
      (s.mulSupport_of_fiberwise_prod_subset_image f Prod.fst),
    ‚Üê Finset.prod_fiberwise_of_maps_to (t := Finset.image Prod.fst s) _ f]
  -- `finish` could close the goal here
  simp only [Finset.mem_image]
  exact fun x hx => ‚ü®x, hx, rfl‚ü©

/-- See also `finprod_mem_finset_product'`. -/
@[to_additive /-- See also `finsum_mem_finset_product'`. -/]
theorem finprod_mem_finset_product (s : Finset (Œ± √ó Œ≤)) (f : Œ± √ó Œ≤ ‚Üí M) :
    (‚àè·∂† (ab) (_ : ab ‚àà s), f ab) = ‚àè·∂† (a) (b) (_ : (a, b) ‚àà s), f (a, b) := by
  classical
    rw [finprod_mem_finset_product']
    simp

@[to_additive]
theorem finprod_mem_finset_product‚ÇÉ {Œ≥ : Type*} (s : Finset (Œ± √ó Œ≤ √ó Œ≥)) (f : Œ± √ó Œ≤ √ó Œ≥ ‚Üí M) :
    (‚àè·∂† (abc) (_ : abc ‚àà s), f abc) = ‚àè·∂† (a) (b) (c) (_ : (a, b, c) ‚àà s), f (a, b, c) := by
  classical
    rw [finprod_mem_finset_product']
    simp_rw [finprod_mem_finset_product']
    simp

@[to_additive]
theorem finprod_curry (f : Œ± √ó Œ≤ ‚Üí M) (hf : (mulSupport f).Finite) :
    ‚àè·∂† ab, f ab = ‚àè·∂† (a) (b), f (a, b) := by
  have h‚ÇÅ : ‚àÄ a, ‚àè·∂† _ : a ‚àà hf.toFinset, f a = f a := by simp
  have h‚ÇÇ : ‚àè·∂† a, f a = ‚àè·∂† (a) (_ : a ‚àà hf.toFinset), f a := by simp
  simp_rw [h‚ÇÇ, finprod_mem_finset_product, h‚ÇÅ]

@[to_additive]
theorem finprod_curry‚ÇÉ {Œ≥ : Type*} (f : Œ± √ó Œ≤ √ó Œ≥ ‚Üí M) (h : (mulSupport f).Finite) :
    ‚àè·∂† abc, f abc = ‚àè·∂† (a) (b) (c), f (a, b, c) := by
  rw [finprod_curry f h]
  congr
  ext a
  rw [finprod_curry]
  simp [h]

@[to_additive]
theorem finprod_dmem {s : Set Œ±} [DecidablePred (¬∑ ‚àà s)] (f : ‚àÄ a : Œ±, a ‚àà s ‚Üí M) :
    (‚àè·∂† (a : Œ±) (h : a ‚àà s), f a h) = ‚àè·∂† (a : Œ±) (_ : a ‚àà s), if h' : a ‚àà s then f a h' else 1 :=
  finprod_congr fun _ => finprod_congr fun ha => (dif_pos ha).symm

@[to_additive]
theorem finprod_emb_domain' {f : Œ± ‚Üí Œ≤} (hf : Injective f) [DecidablePred (¬∑ ‚àà Set.range f)]
    (g : Œ± ‚Üí M) :
    (‚àè·∂† b : Œ≤, if h : b ‚àà Set.range f then g (Classical.choose h) else 1) = ‚àè·∂† a : Œ±, g a := by
  simp_rw [‚Üê finprod_eq_dif]
  rw [finprod_dmem, finprod_mem_range hf, finprod_congr fun a => _]
  intro a
  rw [dif_pos (Set.mem_range_self a), hf (Classical.choose_spec (Set.mem_range_self a))]

@[to_additive]
theorem finprod_emb_domain (f : Œ± ‚Ü™ Œ≤) [DecidablePred (¬∑ ‚àà Set.range f)] (g : Œ± ‚Üí M) :
    (‚àè·∂† b : Œ≤, if h : b ‚àà Set.range f then g (Classical.choose h) else 1) = ‚àè·∂† a : Œ±, g a :=
  finprod_emb_domain' f.injective g

@[simp, norm_cast]
lemma Nat.cast_finprod [Finite Œπ] {R : Type*} [CommSemiring R] (f : Œπ ‚Üí ‚Ñï) :
    ‚Üë(‚àè·∂† x, f x : ‚Ñï) = ‚àè·∂† x, (f x : R) :=
  (Nat.castRingHom R).map_finprod f.mulSupport.toFinite

@[simp, norm_cast]
lemma Nat.cast_finprod_mem {s : Set Œπ} (hs : s.Finite) {R : Type*} [CommSemiring R] (f : Œπ ‚Üí ‚Ñï) :
    ‚Üë(‚àè·∂† x ‚àà s, f x : ‚Ñï) = ‚àè·∂† x ‚àà s, (f x : R) :=
  (Nat.castRingHom R).map_finprod_mem _ hs

@[simp, norm_cast]
lemma Nat.cast_finsum [Finite Œπ] {M : Type*} [AddCommMonoidWithOne M]
    (f : Œπ ‚Üí ‚Ñï) : ‚Üë(‚àë·∂† x, f x : ‚Ñï) = ‚àë·∂† x, (f x : M) :=
  (Nat.castAddMonoidHom M).map_finsum f.support.toFinite

@[simp, norm_cast]
lemma Nat.cast_finsum_mem {s : Set Œπ} (hs : s.Finite) {M : Type*}
    [AddCommMonoidWithOne M] (f : Œπ ‚Üí ‚Ñï) : ‚Üë(‚àë·∂† x ‚àà s, f x : ‚Ñï) = ‚àë·∂† x ‚àà s, (f x : M) :=
  (Nat.castAddMonoidHom M).map_finsum_mem _ hs

end type
