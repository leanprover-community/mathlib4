/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/
import Mathlib.Algebra.Group.Defs
import Mathlib.Logic.Nontrivial
import Mathlib.Algebra.NeZero

#align_import algebra.group_with_zero.defs from "leanprover-community/mathlib"@"2f3994e1b117b1e1da49bcfb67334f33460c3ce4"

/-!
# Typeclasses for groups with an adjoined zero element

This file provides just the typeclass definitions, and the projection lemmas that expose their
members.

## Main definitions

* `GroupWithZero`
* `CommGroupWithZero`
-/


universe u

set_option autoImplicit true

-- We have to fix the universe of `Gâ‚€` here, since the default argument to
-- `GroupWithZero.div'` cannot contain a universe metavariable.
variable {Gâ‚€ : Type u} {Mâ‚€ Mâ‚€' Gâ‚€' : Type*}

-- Porting note:
-- This theorem was introduced during ad-hoc porting
-- and hopefully can be removed again after `Mathlib.Algebra.Ring.Basic` is fully ported.
theorem eq_of_sub_eq_zero' [AddGroup R] {a b : R} (h : a - b = 0) : a = b :=
  add_right_cancel <| show a + (-b) = b + (-b) by rw [â† sub_eq_add_neg, h, add_neg_self]
                                                  -- ðŸŽ‰ no goals

-- Porting note:
-- This theorem was introduced during ad-hoc porting
-- and hopefully can be removed again after `Mathlib.Algebra.Ring.Basic` is fully ported.
theorem pow_succ'' [Monoid M] : âˆ€ (n : â„•) (a : M), a ^ n.succ = a * a ^ n :=
  Monoid.npow_succ

/-- Typeclass for expressing that a type `Mâ‚€` with multiplication and a zero satisfies
`0 * a = 0` and `a * 0 = 0` for all `a : Mâ‚€`. -/
class MulZeroClass (Mâ‚€ : Type u) extends Mul Mâ‚€, Zero Mâ‚€ where
  /-- Zero is a left absorbing element for multiplication -/
  zero_mul : âˆ€ a : Mâ‚€, 0 * a = 0
  /-- Zero is a right absorbing element for multiplication -/
  mul_zero : âˆ€ a : Mâ‚€, a * 0 = 0
#align mul_zero_class MulZeroClass

/-- A mixin for left cancellative multiplication by nonzero elements. -/
class IsLeftCancelMulZero (Mâ‚€ : Type u) [Mul Mâ‚€] [Zero Mâ‚€] : Prop where
  /-- Multiplication by a nonzero element is left cancellative. -/
  protected mul_left_cancel_of_ne_zero : âˆ€ {a b c : Mâ‚€}, a â‰  0 â†’ a * b = a * c â†’ b = c
#align is_left_cancel_mul_zero IsLeftCancelMulZero

section IsLeftCancelMulZero

variable [Mul Mâ‚€] [Zero Mâ‚€] [IsLeftCancelMulZero Mâ‚€] {a b c : Mâ‚€}

theorem mul_left_cancelâ‚€ (ha : a â‰  0) (h : a * b = a * c) : b = c :=
  IsLeftCancelMulZero.mul_left_cancel_of_ne_zero ha h
#align mul_left_cancelâ‚€ mul_left_cancelâ‚€

theorem mul_right_injectiveâ‚€ (ha : a â‰  0) : Function.Injective ((Â· * Â·) a) :=
  fun _ _ => mul_left_cancelâ‚€ ha
#align mul_right_injectiveâ‚€ mul_right_injectiveâ‚€

end IsLeftCancelMulZero

/-- A mixin for right cancellative multiplication by nonzero elements. -/
class IsRightCancelMulZero (Mâ‚€ : Type u) [Mul Mâ‚€] [Zero Mâ‚€] : Prop where
  /-- Multiplicatin by a nonzero element is right cancellative. -/
  protected mul_right_cancel_of_ne_zero : âˆ€ {a b c : Mâ‚€}, b â‰  0 â†’ a * b = c * b â†’ a = c
#align is_right_cancel_mul_zero IsRightCancelMulZero

section IsRightCancelMulZero

variable [Mul Mâ‚€] [Zero Mâ‚€] [IsRightCancelMulZero Mâ‚€] {a b c : Mâ‚€}

theorem mul_right_cancelâ‚€ (hb : b â‰  0) (h : a * b = c * b) : a = c :=
  IsRightCancelMulZero.mul_right_cancel_of_ne_zero hb h
#align mul_right_cancelâ‚€ mul_right_cancelâ‚€

theorem mul_left_injectiveâ‚€ (hb : b â‰  0) : Function.Injective fun a => a * b :=
  fun _ _ => mul_right_cancelâ‚€ hb
#align mul_left_injectiveâ‚€ mul_left_injectiveâ‚€

end IsRightCancelMulZero

/-- A mixin for cancellative multiplication by nonzero elements. -/
class IsCancelMulZero (Mâ‚€ : Type u) [Mul Mâ‚€] [Zero Mâ‚€]
  extends IsLeftCancelMulZero Mâ‚€, IsRightCancelMulZero Mâ‚€ : Prop
#align is_cancel_mul_zero IsCancelMulZero

export MulZeroClass (zero_mul mul_zero)
attribute [simp] zero_mul mul_zero
#align zero_mul MulZeroClass.zero_mul
#align mul_zero MulZeroClass.mul_zero

/-- Predicate typeclass for expressing that `a * b = 0` implies `a = 0` or `b = 0`
for all `a` and `b` of type `Gâ‚€`. -/
class NoZeroDivisors (Mâ‚€ : Type*) [Mul Mâ‚€] [Zero Mâ‚€] : Prop where
  /-- For all `a` and `b` of `Gâ‚€`, `a * b = 0` implies `a = 0` or `b = 0`. -/
  eq_zero_or_eq_zero_of_mul_eq_zero : âˆ€ {a b : Mâ‚€}, a * b = 0 â†’ a = 0 âˆ¨ b = 0
#align no_zero_divisors NoZeroDivisors

export NoZeroDivisors (eq_zero_or_eq_zero_of_mul_eq_zero)
/-- A type `Sâ‚€` is a "semigroup with zeroâ€ if it is a semigroup with zero element, and `0` is left
and right absorbing. -/
class SemigroupWithZero (Sâ‚€ : Type u) extends Semigroup Sâ‚€, MulZeroClass Sâ‚€
#align semigroup_with_zero SemigroupWithZero

/-- A typeclass for non-associative monoids with zero elements. -/
class MulZeroOneClass (Mâ‚€ : Type u) extends MulOneClass Mâ‚€, MulZeroClass Mâ‚€
#align mul_zero_one_class MulZeroOneClass

/-- A type `Mâ‚€` is a â€œmonoid with zeroâ€ if it is a monoid with zero element, and `0` is left
and right absorbing. -/
class MonoidWithZero (Mâ‚€ : Type u) extends Monoid Mâ‚€, MulZeroOneClass Mâ‚€, SemigroupWithZero Mâ‚€
#align monoid_with_zero MonoidWithZero

/-- A type `M` is a `CancelMonoidWithZero` if it is a monoid with zero element, `0` is left
and right absorbing, and left/right multiplication by a non-zero element is injective. -/
class CancelMonoidWithZero (Mâ‚€ : Type*) extends MonoidWithZero Mâ‚€, IsCancelMulZero Mâ‚€
#align cancel_monoid_with_zero CancelMonoidWithZero

/-- A type `M` is a commutative â€œmonoid with zeroâ€ if it is a commutative monoid with zero
element, and `0` is left and right absorbing. -/
class CommMonoidWithZero (Mâ‚€ : Type*) extends CommMonoid Mâ‚€, MonoidWithZero Mâ‚€
#align comm_monoid_with_zero CommMonoidWithZero

section CommSemigroup

variable [CommSemigroup Mâ‚€] [Zero Mâ‚€]

lemma IsLeftCancelMulZero.to_isRightCancelMulZero [IsLeftCancelMulZero Mâ‚€] :
    IsRightCancelMulZero Mâ‚€ :=
{ mul_right_cancel_of_ne_zero :=
    fun hb h => mul_left_cancelâ‚€ hb <| (mul_comm _ _).trans (h.trans (mul_comm _ _)) }
#align is_left_cancel_mul_zero.to_is_right_cancel_mul_zero IsLeftCancelMulZero.to_isRightCancelMulZero

lemma IsRightCancelMulZero.to_isLeftCancelMulZero [IsRightCancelMulZero Mâ‚€] :
    IsLeftCancelMulZero Mâ‚€ :=
{ mul_left_cancel_of_ne_zero :=
    fun hb h => mul_right_cancelâ‚€ hb <| (mul_comm _ _).trans (h.trans (mul_comm _ _)) }
#align is_right_cancel_mul_zero.to_is_left_cancel_mul_zero IsRightCancelMulZero.to_isLeftCancelMulZero

lemma IsLeftCancelMulZero.to_isCancelMulZero [IsLeftCancelMulZero Mâ‚€] :
    IsCancelMulZero Mâ‚€ :=
{ IsLeftCancelMulZero.to_isRightCancelMulZero with }
#align is_left_cancel_mul_zero.to_is_cancel_mul_zero IsLeftCancelMulZero.to_isCancelMulZero

lemma IsRightCancelMulZero.to_isCancelMulZero [IsRightCancelMulZero Mâ‚€] :
    IsCancelMulZero Mâ‚€ :=
{ IsRightCancelMulZero.to_isLeftCancelMulZero with }
#align is_right_cancel_mul_zero.to_is_cancel_mul_zero IsRightCancelMulZero.to_isCancelMulZero

end CommSemigroup

/-- A type `M` is a `CancelCommMonoidWithZero` if it is a commutative monoid with zero element,
 `0` is left and right absorbing,
  and left/right multiplication by a non-zero element is injective. -/
class CancelCommMonoidWithZero (Mâ‚€ : Type*) extends CommMonoidWithZero Mâ‚€, IsLeftCancelMulZero Mâ‚€
#align cancel_comm_monoid_with_zero CancelCommMonoidWithZero

-- See note [lower cancel priority]
attribute [instance 75] CancelCommMonoidWithZero.toCommMonoidWithZero

instance (priority := 100) CancelCommMonoidWithZero.toCancelMonoidWithZero
    [CancelCommMonoidWithZero Mâ‚€] : CancelMonoidWithZero Mâ‚€ :=
{ IsLeftCancelMulZero.to_isCancelMulZero (Mâ‚€ := Mâ‚€) with }

/-- A type `Gâ‚€` is a â€œgroup with zeroâ€ if it is a monoid with zero element (distinct from `1`)
such that every nonzero element is invertible.
The type is required to come with an â€œinverseâ€ function, and the inverse of `0` must be `0`.

Examples include division rings and the ordered monoids that are the
target of valuations in general valuation theory.-/
class GroupWithZero (Gâ‚€ : Type u) extends MonoidWithZero Gâ‚€, DivInvMonoid Gâ‚€, Nontrivial Gâ‚€ where
  /-- The inverse of `0` in a group with zero is `0`. -/
  inv_zero : (0 : Gâ‚€)â»Â¹ = 0
  /-- Every nonzero element of a group with zero is invertible. -/
  mul_inv_cancel (a : Gâ‚€) : a â‰  0 â†’ a * aâ»Â¹ = 1
#align group_with_zero GroupWithZero

export GroupWithZero (inv_zero)
attribute [simp] inv_zero

@[simp] lemma mul_inv_cancel [GroupWithZero Gâ‚€] {a : Gâ‚€} (h : a â‰  0) : a * aâ»Â¹ = 1 :=
  GroupWithZero.mul_inv_cancel a h
#align mul_inv_cancel mul_inv_cancel

/-- A type `Gâ‚€` is a commutative â€œgroup with zeroâ€
if it is a commutative monoid with zero element (distinct from `1`)
such that every nonzero element is invertible.
The type is required to come with an â€œinverseâ€ function, and the inverse of `0` must be `0`. -/
class CommGroupWithZero (Gâ‚€ : Type*) extends CommMonoidWithZero Gâ‚€, GroupWithZero Gâ‚€
#align comm_group_with_zero CommGroupWithZero

section NeZero

variable [MulZeroOneClass Mâ‚€] [Nontrivial Mâ‚€] {a b : Mâ‚€}

variable (Mâ‚€)

/-- In a nontrivial monoid with zero, zero and one are different. -/
instance NeZero.one : NeZero (1 : Mâ‚€) := âŸ¨by
  intro h
  -- âŠ¢ False
  rcases exists_pair_ne Mâ‚€ with âŸ¨x, y, hxâŸ©
  -- âŠ¢ False
  apply hx
  -- âŠ¢ x = y
  calc
    x = 1 * x := by rw [one_mul]
    _ = 0 := by rw [h, zero_mul]
    _ = 1 * y := by rw [h, zero_mul]
    _ = y := by rw [one_mul]âŸ©
#align ne_zero.one NeZero.one

variable {Mâ‚€}

/-- Pullback a `Nontrivial` instance along a function sending `0` to `0` and `1` to `1`. -/
theorem pullback_nonzero [Zero Mâ‚€'] [One Mâ‚€'] (f : Mâ‚€' â†’ Mâ‚€) (zero : f 0 = 0) (one : f 1 = 1) :
    Nontrivial Mâ‚€' :=
  âŸ¨âŸ¨0, 1, mt (congr_arg f) <| by
    rw [zero, one]
    -- âŠ¢ Â¬0 = 1
    exact zero_ne_oneâŸ©âŸ©
    -- ðŸŽ‰ no goals
#align pullback_nonzero pullback_nonzero

end NeZero

section MulZeroClass

variable [MulZeroClass Mâ‚€]

theorem mul_eq_zero_of_left {a : Mâ‚€} (h : a = 0) (b : Mâ‚€) : a * b = 0 := h.symm â–¸ zero_mul b
#align mul_eq_zero_of_left mul_eq_zero_of_left

theorem mul_eq_zero_of_right (a : Mâ‚€) {b : Mâ‚€} (h : b = 0) : a * b = 0 := h.symm â–¸ mul_zero a
#align mul_eq_zero_of_right mul_eq_zero_of_right

variable [NoZeroDivisors Mâ‚€] {a b : Mâ‚€}

/-- If `Î±` has no zero divisors, then the product of two elements equals zero iff one of them
equals zero. -/
@[simp]
theorem mul_eq_zero : a * b = 0 â†” a = 0 âˆ¨ b = 0 :=
  âŸ¨eq_zero_or_eq_zero_of_mul_eq_zero,
    fun o => o.elim (fun h => mul_eq_zero_of_left h b) (mul_eq_zero_of_right a)âŸ©
#align mul_eq_zero mul_eq_zero

/-- If `Î±` has no zero divisors, then the product of two elements equals zero iff one of them
equals zero. -/
@[simp]
theorem zero_eq_mul : 0 = a * b â†” a = 0 âˆ¨ b = 0 := by rw [eq_comm, mul_eq_zero]
                                                      -- ðŸŽ‰ no goals
#align zero_eq_mul zero_eq_mul

/-- If `Î±` has no zero divisors, then the product of two elements is nonzero iff both of them
are nonzero. -/
theorem mul_ne_zero_iff : a * b â‰  0 â†” a â‰  0 âˆ§ b â‰  0 := mul_eq_zero.not.trans not_or
#align mul_ne_zero_iff mul_ne_zero_iff

/-- If `Î±` has no zero divisors, then for elements `a, b : Î±`, `a * b` equals zero iff so is
`b * a`. -/
theorem mul_eq_zero_comm : a * b = 0 â†” b * a = 0 :=
  mul_eq_zero.trans <| or_comm.trans mul_eq_zero.symm
#align mul_eq_zero_comm mul_eq_zero_comm

/-- If `Î±` has no zero divisors, then for elements `a, b : Î±`, `a * b` is nonzero iff so is
`b * a`. -/
theorem mul_ne_zero_comm : a * b â‰  0 â†” b * a â‰  0 := mul_eq_zero_comm.not
#align mul_ne_zero_comm mul_ne_zero_comm

theorem mul_self_eq_zero : a * a = 0 â†” a = 0 := by simp
                                                   -- ðŸŽ‰ no goals
#align mul_self_eq_zero mul_self_eq_zero

theorem zero_eq_mul_self : 0 = a * a â†” a = 0 := by simp
                                                   -- ðŸŽ‰ no goals
#align zero_eq_mul_self zero_eq_mul_self

theorem mul_self_ne_zero : a * a â‰  0 â†” a â‰  0 := mul_self_eq_zero.not
#align mul_self_ne_zero mul_self_ne_zero

theorem zero_ne_mul_self : 0 â‰  a * a â†” a â‰  0 := zero_eq_mul_self.not
#align zero_ne_mul_self zero_ne_mul_self

end MulZeroClass
