/-
Copyright (c) 2021 Damiano Testa. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Damiano Testa
-/
import Mathlib.Algebra.Group.Defs
import Mathlib.Order.Basic
import Mathlib.Order.Monotone.Basic

#align_import algebra.covariant_and_contravariant from "leanprover-community/mathlib"@"2258b40dacd2942571c8ce136215350c702dc78f"

/-!

# Covariants and contravariants

This file contains general lemmas and instances to work with the interactions between a relation and
an action on a Type.

The intended application is the splitting of the ordering from the algebraic assumptions on the
operations in the `Ordered[...]` hierarchy.

The strategy is to introduce two more flexible typeclasses, `CovariantClass` and
`ContravariantClass`:

* `CovariantClass` models the implication `a ‚â§ b ‚Üí c * a ‚â§ c * b` (multiplication is monotone),
* `ContravariantClass` models the implication `a * b < a * c ‚Üí b < c`.

Since `Co(ntra)variantClass` takes as input the operation (typically `(+)` or `(*)`) and the order
relation (typically `(‚â§)` or `(<)`), these are the only two typeclasses that I have used.

The general approach is to formulate the lemma that you are interested in and prove it, with the
`Ordered[...]` typeclass of your liking.  After that, you convert the single typeclass,
say `[OrderedCancelMonoid M]`, into three typeclasses, e.g.
`[LeftCancelSemigroup M] [PartialOrder M] [CovariantClass M M (Function.swap (*)) (‚â§)]`
and have a go at seeing if the proof still works!

Note that it is possible to combine several `Co(ntra)variantClass` assumptions together.
Indeed, the usual ordered typeclasses arise from assuming the pair
`[CovariantClass M M (*) (‚â§)] [ContravariantClass M M (*) (<)]`
on top of order/algebraic assumptions.

A formal remark is that normally `CovariantClass` uses the `(‚â§)`-relation, while
`ContravariantClass` uses the `(<)`-relation. This need not be the case in general, but seems to be
the most common usage. In the opposite direction, the implication
```lean
[Semigroup Œ±] [PartialOrder Œ±] [ContravariantClass Œ± Œ± (*) (‚â§)] => LeftCancelSemigroup Œ±
```
holds -- note the `Co*ntra*` assumption on the `(‚â§)`-relation.

# Formalization notes

We stick to the convention of using `Function.swap (*)` (or `Function.swap (+)`), for the
typeclass assumptions, since `Function.swap` is slightly better behaved than `flip`.
However, sometimes as a **non-typeclass** assumption, we prefer `flip (*)` (or `flip (+)`),
as it is easier to use.

-/


-- TODO: convert `ExistsMulOfLE`, `ExistsAddOfLE`?
-- TODO: relationship with `Con/AddCon`
-- TODO: include equivalence of `LeftCancelSemigroup` with
-- `Semigroup PartialOrder ContravariantClass Œ± Œ± (*) (‚â§)`?
-- TODO : use ‚áí, as per Eric's suggestion?  See
-- https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/ordered.20stuff/near/236148738
-- for a discussion.
open Function

section Variants

variable {M N : Type*} (Œº : M ‚Üí N ‚Üí N) (r : N ‚Üí N ‚Üí Prop)

variable (M N)

/-- `Covariant` is useful to formulate succintly statements about the interactions between an
action of a Type on another one and a relation on the acted-upon Type.

See the `CovariantClass` doc-string for its meaning. -/
def Covariant : Prop :=
  ‚àÄ (m) {n‚ÇÅ n‚ÇÇ}, r n‚ÇÅ n‚ÇÇ ‚Üí r (Œº m n‚ÇÅ) (Œº m n‚ÇÇ)
#align covariant Covariant

/-- `Contravariant` is useful to formulate succintly statements about the interactions between an
action of a Type on another one and a relation on the acted-upon Type.

See the `ContravariantClass` doc-string for its meaning. -/
def Contravariant : Prop :=
  ‚àÄ (m) {n‚ÇÅ n‚ÇÇ}, r (Œº m n‚ÇÅ) (Œº m n‚ÇÇ) ‚Üí r n‚ÇÅ n‚ÇÇ
#align contravariant Contravariant

/-- Given an action `Œº` of a Type `M` on a Type `N` and a relation `r` on `N`, informally, the
`CovariantClass` says that "the action `Œº` preserves the relation `r`."

More precisely, the `CovariantClass` is a class taking two Types `M N`, together with an "action"
`Œº : M ‚Üí N ‚Üí N` and a relation `r : N ‚Üí N ‚Üí Prop`.  Its unique field `elim` is the assertion that
for all `m ‚àà M` and all elements `n‚ÇÅ, n‚ÇÇ ‚àà N`, if the relation `r` holds for the pair
`(n‚ÇÅ, n‚ÇÇ)`, then, the relation `r` also holds for the pair `(Œº m n‚ÇÅ, Œº m n‚ÇÇ)`,
obtained from `(n‚ÇÅ, n‚ÇÇ)` by acting upon it by `m`.

If `m : M` and `h : r n‚ÇÅ n‚ÇÇ`, then `CovariantClass.elim m h : r (Œº m n‚ÇÅ) (Œº m n‚ÇÇ)`.
-/
class CovariantClass : Prop where
  /-- For all `m ‚àà M` and all elements `n‚ÇÅ, n‚ÇÇ ‚àà N`, if the relation `r` holds for the pair
  `(n‚ÇÅ, n‚ÇÇ)`, then, the relation `r` also holds for the pair `(Œº m n‚ÇÅ, Œº m n‚ÇÇ)` -/
  protected elim : Covariant M N Œº r
#align covariant_class CovariantClass

/-- Given an action `Œº` of a Type `M` on a Type `N` and a relation `r` on `N`, informally, the
`ContravariantClass` says that "if the result of the action `Œº` on a pair satisfies the
relation `r`, then the initial pair satisfied the relation `r`."

More precisely, the `ContravariantClass` is a class taking two Types `M N`, together with an
"action" `Œº : M ‚Üí N ‚Üí N` and a relation `r : N ‚Üí N ‚Üí Prop`.  Its unique field `elim` is the
assertion that for all `m ‚àà M` and all elements `n‚ÇÅ, n‚ÇÇ ‚àà N`, if the relation `r` holds for the
pair `(Œº m n‚ÇÅ, Œº m n‚ÇÇ)` obtained from `(n‚ÇÅ, n‚ÇÇ)` by acting upon it by `m`, then, the relation
`r` also holds for the pair `(n‚ÇÅ, n‚ÇÇ)`.

If `m : M` and `h : r (Œº m n‚ÇÅ) (Œº m n‚ÇÇ)`, then `ContravariantClass.elim m h : r n‚ÇÅ n‚ÇÇ`.
-/
class ContravariantClass : Prop where
  /-- For all `m ‚àà M` and all elements `n‚ÇÅ, n‚ÇÇ ‚àà N`, if the relation `r` holds for the
  pair `(Œº m n‚ÇÅ, Œº m n‚ÇÇ)` obtained from `(n‚ÇÅ, n‚ÇÇ)` by acting upon it by `m`, then, the relation
  `r` also holds for the pair `(n‚ÇÅ, n‚ÇÇ)`. -/
  protected elim : Contravariant M N Œº r
#align contravariant_class ContravariantClass

theorem rel_iff_cov [CovariantClass M N Œº r] [ContravariantClass M N Œº r] (m : M) {a b : N} :
    r (Œº m a) (Œº m b) ‚Üî r a b :=
  ‚ü®ContravariantClass.elim _, CovariantClass.elim _‚ü©
#align rel_iff_cov rel_iff_cov

section flip

variable {M N Œº r}

theorem Covariant.flip (h : Covariant M N Œº r) : Covariant M N Œº (flip r) :=
  fun a _ _ hbc ‚Ü¶ h a hbc
#align covariant.flip Covariant.flip

theorem Contravariant.flip (h : Contravariant M N Œº r) : Contravariant M N Œº (flip r) :=
  fun a _ _ hbc ‚Ü¶ h a hbc
#align contravariant.flip Contravariant.flip

end flip

section Covariant

variable {M N Œº r} [CovariantClass M N Œº r]

theorem act_rel_act_of_rel (m : M) {a b : N} (ab : r a b) : r (Œº m a) (Œº m b) :=
  CovariantClass.elim _ ab
#align act_rel_act_of_rel act_rel_act_of_rel

@[to_additive]
theorem Group.covariant_iff_contravariant [Group N] :
    Covariant N N (¬∑ * ¬∑) r ‚Üî Contravariant N N (¬∑ * ¬∑) r := by
  refine ‚ü®fun h a b c bc ‚Ü¶ ?_, fun h a b c bc ‚Ü¶ ?_‚ü©
  -- ‚ä¢ r b c
  ¬∑ rw [‚Üê inv_mul_cancel_left a b, ‚Üê inv_mul_cancel_left a c]
    -- ‚ä¢ r (a‚Åª¬π * (a * b)) (a‚Åª¬π * (a * c))
    exact h a‚Åª¬π bc
    -- üéâ no goals
  ¬∑ rw [‚Üê inv_mul_cancel_left a b, ‚Üê inv_mul_cancel_left a c] at bc
    -- ‚ä¢ r ((fun x x_1 => x * x_1) a b) ((fun x x_1 => x * x_1) a c)
    exact h a‚Åª¬π bc
    -- üéâ no goals
#align group.covariant_iff_contravariant Group.covariant_iff_contravariant
#align add_group.covariant_iff_contravariant AddGroup.covariant_iff_contravariant

@[to_additive]
instance (priority := 100) Group.covconv [Group N] [CovariantClass N N (¬∑ * ¬∑) r] :
    ContravariantClass N N (¬∑ * ¬∑) r :=
  ‚ü®Group.covariant_iff_contravariant.mp CovariantClass.elim‚ü©

@[to_additive]
theorem Group.covariant_swap_iff_contravariant_swap [Group N] :
    Covariant N N (swap (¬∑ * ¬∑)) r ‚Üî Contravariant N N (swap (¬∑ * ¬∑)) r := by
  refine ‚ü®fun h a b c bc ‚Ü¶ ?_, fun h a b c bc ‚Ü¶ ?_‚ü©
  -- ‚ä¢ r b c
  ¬∑ rw [‚Üê mul_inv_cancel_right b a, ‚Üê mul_inv_cancel_right c a]
    -- ‚ä¢ r (b * a * a‚Åª¬π) (c * a * a‚Åª¬π)
    exact h a‚Åª¬π bc
    -- üéâ no goals
  ¬∑ rw [‚Üê mul_inv_cancel_right b a, ‚Üê mul_inv_cancel_right c a] at bc
    -- ‚ä¢ r (swap (fun x x_1 => x * x_1) a b) (swap (fun x x_1 => x * x_1) a c)
    exact h a‚Åª¬π bc
    -- üéâ no goals
#align group.covariant_swap_iff_contravariant_swap Group.covariant_swap_iff_contravariant_swap
#align add_group.covariant_swap_iff_contravariant_swap AddGroup.covariant_swap_iff_contravariant_swap


@[to_additive]
instance (priority := 100) Group.covconv_swap [Group N] [CovariantClass N N (swap (¬∑ * ¬∑)) r] :
    ContravariantClass N N (swap (¬∑ * ¬∑)) r :=
  ‚ü®Group.covariant_swap_iff_contravariant_swap.mp CovariantClass.elim‚ü©


section Trans

variable [IsTrans N r] (m n : M) {a b c d : N}

--  Lemmas with 3 elements.
theorem act_rel_of_rel_of_act_rel (ab : r a b) (rl : r (Œº m b) c) : r (Œº m a) c :=
  _root_.trans (act_rel_act_of_rel m ab) rl
#align act_rel_of_rel_of_act_rel act_rel_of_rel_of_act_rel

theorem rel_act_of_rel_of_rel_act (ab : r a b) (rr : r c (Œº m a)) : r c (Œº m b) :=
  _root_.trans rr (act_rel_act_of_rel _ ab)
#align rel_act_of_rel_of_rel_act rel_act_of_rel_of_rel_act

end Trans

end Covariant

--  Lemma with 4 elements.
section MEqN

variable {M N Œº r} {mu : N ‚Üí N ‚Üí N} [IsTrans N r] [i : CovariantClass N N mu r]
  [i' : CovariantClass N N (swap mu) r] {a b c d : N}

theorem act_rel_act_of_rel_of_rel (ab : r a b) (cd : r c d) : r (mu a c) (mu b d) :=
  _root_.trans (@act_rel_act_of_rel _ _ (swap mu) r _ c _ _ ab) (act_rel_act_of_rel b cd)
#align act_rel_act_of_rel_of_rel act_rel_act_of_rel_of_rel

end MEqN

section Contravariant

variable {M N Œº r} [ContravariantClass M N Œº r]

theorem rel_of_act_rel_act (m : M) {a b : N} (ab : r (Œº m a) (Œº m b)) : r a b :=
  ContravariantClass.elim _ ab
#align rel_of_act_rel_act rel_of_act_rel_act

section Trans

variable [IsTrans N r] (m n : M) {a b c d : N}

--  Lemmas with 3 elements.
theorem act_rel_of_act_rel_of_rel_act_rel (ab : r (Œº m a) b) (rl : r (Œº m b) (Œº m c)) :
    r (Œº m a) c :=
  _root_.trans ab (rel_of_act_rel_act m rl)
#align act_rel_of_act_rel_of_rel_act_rel act_rel_of_act_rel_of_rel_act_rel

theorem rel_act_of_act_rel_act_of_rel_act (ab : r (Œº m a) (Œº m b)) (rr : r b (Œº m c)) :
    r a (Œº m c) :=
  _root_.trans (rel_of_act_rel_act m ab) rr
#align rel_act_of_act_rel_act_of_rel_act rel_act_of_act_rel_act_of_rel_act

end Trans

end Contravariant

section Monotone

variable {Œ± : Type*} {M N Œº} [Preorder Œ±] [Preorder N]

variable {f : N ‚Üí Œ±}

/-- The partial application of a constant to a covariant operator is monotone. -/
theorem Covariant.monotone_of_const [CovariantClass M N Œº (¬∑ ‚â§ ¬∑)] (m : M) : Monotone (Œº m) :=
  fun _ _ ha ‚Ü¶ CovariantClass.elim m ha
#align covariant.monotone_of_const Covariant.monotone_of_const

/-- A monotone function remains monotone when composed with the partial application
of a covariant operator. E.g., `‚àÄ (m : ‚Ñï), Monotone f ‚Üí Monotone (fun n ‚Ü¶ f (m + n))`. -/
theorem Monotone.covariant_of_const [CovariantClass M N Œº (¬∑ ‚â§ ¬∑)] (hf : Monotone f) (m : M) :
    Monotone fun n ‚Ü¶ f (Œº m n) :=
  fun _ _ x ‚Ü¶ hf (Covariant.monotone_of_const m x)
#align monotone.covariant_of_const Monotone.covariant_of_const

/-- Same as `Monotone.covariant_of_const`, but with the constant on the other side of
the operator.  E.g., `‚àÄ (m : ‚Ñï), Monotone f ‚Üí Monotone (fun n ‚Ü¶ f (n + m))`. -/
theorem Monotone.covariant_of_const' {Œº : N ‚Üí N ‚Üí N} [CovariantClass N N (swap Œº) (¬∑ ‚â§ ¬∑)]
    (hf : Monotone f) (m : N) : Monotone fun n ‚Ü¶ f (Œº n m) :=
  fun _ _ x ‚Ü¶ hf (@Covariant.monotone_of_const _ _ (swap Œº) _ _ m _ _ x)
#align monotone.covariant_of_const' Monotone.covariant_of_const'

/-- Dual of `Monotone.covariant_of_const` -/
theorem Antitone.covariant_of_const [CovariantClass M N Œº (¬∑ ‚â§ ¬∑)] (hf : Antitone f) (m : M) :
    Antitone fun n ‚Ü¶ f (Œº m n) :=
  hf.comp_monotone <| Covariant.monotone_of_const m
#align antitone.covariant_of_const Antitone.covariant_of_const

/-- Dual of `Monotone.covariant_of_const'` -/
theorem Antitone.covariant_of_const' {Œº : N ‚Üí N ‚Üí N} [CovariantClass N N (swap Œº) (¬∑ ‚â§ ¬∑)]
    (hf : Antitone f) (m : N) : Antitone fun n ‚Ü¶ f (Œº n m) :=
  hf.comp_monotone <| @Covariant.monotone_of_const _ _ (swap Œº) _ _ m
#align antitone.covariant_of_const' Antitone.covariant_of_const'

end Monotone

theorem covariant_le_of_covariant_lt [PartialOrder N] :
    Covariant M N Œº (¬∑ < ¬∑) ‚Üí Covariant M N Œº (¬∑ ‚â§ ¬∑) := by
  intro h a b c bc
  -- ‚ä¢ Œº a b ‚â§ Œº a c
  rcases le_iff_eq_or_lt.mp bc with (rfl | bc)
  -- ‚ä¢ Œº a b ‚â§ Œº a b
  ¬∑ exact rfl.le
    -- üéâ no goals
  ¬∑ exact (h _ bc).le
    -- üéâ no goals
#align covariant_le_of_covariant_lt covariant_le_of_covariant_lt

theorem contravariant_lt_of_contravariant_le [PartialOrder N] :
    Contravariant M N Œº (¬∑ ‚â§ ¬∑) ‚Üí Contravariant M N Œº (¬∑ < ¬∑) := by
  refine fun h a b c bc ‚Ü¶ lt_iff_le_and_ne.mpr ‚ü®h a bc.le, ?_‚ü©
  -- ‚ä¢ b ‚â† c
  rintro rfl; exact lt_irrefl _ bc
  -- ‚ä¢ False
              -- üéâ no goals
#align contravariant_lt_of_contravariant_le contravariant_lt_of_contravariant_le

theorem covariant_le_iff_contravariant_lt [LinearOrder N] :
    Covariant M N Œº (¬∑ ‚â§ ¬∑) ‚Üî Contravariant M N Œº (¬∑ < ¬∑) :=
  ‚ü®fun h _ _ _ bc ‚Ü¶ not_le.mp fun k ‚Ü¶ not_le.mpr bc (h _ k),
   fun h _ _ _ bc ‚Ü¶ not_lt.mp fun k ‚Ü¶ not_lt.mpr bc (h _ k)‚ü©
#align covariant_le_iff_contravariant_lt covariant_le_iff_contravariant_lt

theorem covariant_lt_iff_contravariant_le [LinearOrder N] :
    Covariant M N Œº (¬∑ < ¬∑) ‚Üî Contravariant M N Œº (¬∑ ‚â§ ¬∑) :=
  ‚ü®fun h _ _ _ bc ‚Ü¶ not_lt.mp fun k ‚Ü¶ not_lt.mpr bc (h _ k),
   fun h _ _ _ bc ‚Ü¶ not_le.mp fun k ‚Ü¶ not_le.mpr bc (h _ k)‚ü©
#align covariant_lt_iff_contravariant_le covariant_lt_iff_contravariant_le

-- Porting note: `covariant_flip_mul_iff` used to use the `IsSymmOp` typeclass from Lean 3 core.
-- To avoid it, we prove the relevant lemma here.
@[to_additive]
lemma flip_mul [CommSemigroup N] : (flip (¬∑ * ¬∑) : N ‚Üí N ‚Üí N) = (¬∑ * ¬∑) :=
  funext fun a ‚Ü¶ funext fun b ‚Ü¶ mul_comm b a

@[to_additive]
theorem covariant_flip_mul_iff [CommSemigroup N] :
    Covariant N N (flip (¬∑ * ¬∑)) r ‚Üî Covariant N N (¬∑ * ¬∑) r := by rw [flip_mul]
                                                                   -- üéâ no goals
#align covariant_flip_mul_iff covariant_flip_mul_iff
#align covariant_flip_add_iff covariant_flip_add_iff

@[to_additive]
theorem contravariant_flip_mul_iff [CommSemigroup N] :
    Contravariant N N (flip (¬∑ * ¬∑)) r ‚Üî Contravariant N N (¬∑ * ¬∑) r := by rw [flip_mul]
                                                                           -- üéâ no goals
#align contravariant_flip_mul_iff contravariant_flip_mul_iff
#align contravariant_flip_add_iff contravariant_flip_add_iff

@[to_additive]
instance contravariant_mul_lt_of_covariant_mul_le [Mul N] [LinearOrder N]
    [CovariantClass N N (¬∑ * ¬∑) (¬∑ ‚â§ ¬∑)] : ContravariantClass N N (¬∑ * ¬∑) (¬∑ < ¬∑) where
  elim := (covariant_le_iff_contravariant_lt N N (¬∑ * ¬∑)).mp CovariantClass.elim

@[to_additive]
instance covariant_mul_lt_of_contravariant_mul_le [Mul N] [LinearOrder N]
    [ContravariantClass N N (¬∑ * ¬∑) (¬∑ ‚â§ ¬∑)] : CovariantClass N N (¬∑ * ¬∑) (¬∑ < ¬∑) where
  elim := (covariant_lt_iff_contravariant_le N N (¬∑ * ¬∑)).mpr ContravariantClass.elim

@[to_additive]
instance covariant_swap_mul_le_of_covariant_mul_le [CommSemigroup N] [LE N]
    [CovariantClass N N (¬∑ * ¬∑) (¬∑ ‚â§ ¬∑)] : CovariantClass N N (swap (¬∑ * ¬∑)) (¬∑ ‚â§ ¬∑) where
  elim := (covariant_flip_mul_iff N (¬∑ ‚â§ ¬∑)).mpr CovariantClass.elim

@[to_additive]
instance contravariant_swap_mul_le_of_contravariant_mul_le [CommSemigroup N] [LE N]
    [ContravariantClass N N (¬∑ * ¬∑) (¬∑ ‚â§ ¬∑)] : ContravariantClass N N (swap (¬∑ * ¬∑)) (¬∑ ‚â§ ¬∑) where
  elim := (contravariant_flip_mul_iff N (¬∑ ‚â§ ¬∑)).mpr ContravariantClass.elim

@[to_additive]
instance contravariant_swap_mul_lt_of_contravariant_mul_lt [CommSemigroup N] [LT N]
    [ContravariantClass N N (¬∑ * ¬∑) (¬∑ < ¬∑)] : ContravariantClass N N (swap (¬∑ * ¬∑)) (¬∑ < ¬∑) where
  elim := (contravariant_flip_mul_iff N (¬∑ < ¬∑)).mpr ContravariantClass.elim

@[to_additive]
instance covariant_swap_mul_lt_of_covariant_mul_lt [CommSemigroup N] [LT N]
    [CovariantClass N N (¬∑ * ¬∑) (¬∑ < ¬∑)] : CovariantClass N N (swap (¬∑ * ¬∑)) (¬∑ < ¬∑) where
  elim := (covariant_flip_mul_iff N (¬∑ < ¬∑)).mpr CovariantClass.elim

@[to_additive]
instance LeftCancelSemigroup.covariant_mul_lt_of_covariant_mul_le [LeftCancelSemigroup N]
    [PartialOrder N] [CovariantClass N N (¬∑ * ¬∑) (¬∑ ‚â§ ¬∑)] :
    CovariantClass N N (¬∑ * ¬∑) (¬∑ < ¬∑) where
  elim a b c bc := by
    cases' lt_iff_le_and_ne.mp bc with bc cb
    -- ‚ä¢ a * b < a * c
    exact lt_iff_le_and_ne.mpr ‚ü®CovariantClass.elim a bc, (mul_ne_mul_right a).mpr cb‚ü©
    -- üéâ no goals

@[to_additive]
instance RightCancelSemigroup.covariant_swap_mul_lt_of_covariant_swap_mul_le
    [RightCancelSemigroup N] [PartialOrder N] [CovariantClass N N (swap (¬∑ * ¬∑)) (¬∑ ‚â§ ¬∑)] :
    CovariantClass N N (swap (¬∑ * ¬∑)) (¬∑ < ¬∑) where
  elim a b c bc := by
    cases' lt_iff_le_and_ne.mp bc with bc cb
    -- ‚ä¢ swap (fun x x_1 => x * x_1) a b < swap (fun x x_1 => x * x_1) a c
    exact lt_iff_le_and_ne.mpr ‚ü®CovariantClass.elim a bc, (mul_ne_mul_left a).mpr cb‚ü©
    -- üéâ no goals

@[to_additive]
instance LeftCancelSemigroup.contravariant_mul_le_of_contravariant_mul_lt [LeftCancelSemigroup N]
    [PartialOrder N] [ContravariantClass N N (¬∑ * ¬∑) (¬∑ < ¬∑)] :
    ContravariantClass N N (¬∑ * ¬∑) (¬∑ ‚â§ ¬∑) where
  elim a b c bc := by
    cases' le_iff_eq_or_lt.mp bc with h h
    -- ‚ä¢ b ‚â§ c
    ¬∑ exact ((mul_right_inj a).mp h).le
      -- üéâ no goals
    ¬∑ exact (ContravariantClass.elim _ h).le
      -- üéâ no goals

@[to_additive]
instance RightCancelSemigroup.contravariant_swap_mul_le_of_contravariant_swap_mul_lt
    [RightCancelSemigroup N] [PartialOrder N] [ContravariantClass N N (swap (¬∑ * ¬∑)) (¬∑ < ¬∑)] :
    ContravariantClass N N (swap (¬∑ * ¬∑)) (¬∑ ‚â§ ¬∑) where
  elim a b c bc := by
    cases' le_iff_eq_or_lt.mp bc with h h
    -- ‚ä¢ b ‚â§ c
    ¬∑ exact ((mul_left_inj a).mp h).le
      -- üéâ no goals
    ¬∑ exact (ContravariantClass.elim _ h).le
      -- üéâ no goals

end Variants
