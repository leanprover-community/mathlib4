/-
Copyright (c) 2022 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.Algebra.MonoidAlgebra.Basic
import Mathlib.Data.Finsupp.Order

#align_import algebra.monoid_algebra.division from "leanprover-community/mathlib"@"72c366d0475675f1309d3027d3d7d47ee4423951"

/-!
# Division of `AddMonoidAlgebra` by monomials

This file is most important for when `G = â„•` (polynomials) or `G = Ïƒ â†’â‚€ â„•` (multivariate
polynomials).

In order to apply in maximal generality (such as for `LaurentPolynomial`s), this uses
`âˆƒ d, g' = g + d` in many places instead of `g â‰¤ g'`.

## Main definitions

* `AddMonoidAlgebra.divOf x g`: divides `x` by the monomial `AddMonoidAlgebra.of k G g`
* `AddMonoidAlgebra.modOf x g`: the remainder upon dividing `x` by the monomial
  `AddMonoidAlgebra.of k G g`.

## Main results

* `AddMonoidAlgebra.divOf_add_modOf`, `AddMonoidAlgebra.modOf_add_divOf`: `divOf` and
  `modOf` are well-behaved as quotient and remainder operators.

## Implementation notes

`âˆƒ d, g' = g + d` is used as opposed to some other permutation up to commutativity in order to match
the definition of `semigroupDvd`. The results in this file could be duplicated for
`MonoidAlgebra` by using `g âˆ£ g'`, but this can't be done automatically, and in any case is not
likely to be very useful.

-/


variable {k G : Type*} [Semiring k]

namespace AddMonoidAlgebra

section

variable [AddCancelCommMonoid G]

/-- Divide by `of' k G g`, discarding terms not divisible by this. -/
noncomputable def divOf (x : AddMonoidAlgebra k G) (g : G) : AddMonoidAlgebra k G :=
  -- note: comapping by `+ g` has the effect of subtracting `g` from every element in
  -- the support, and discarding the elements of the support from which `g` can't be subtracted.
  -- If `G` is an additive group, such as `â„¤` when used for `LaurentPolynomial`,
  -- then no discarding occurs.
  @Finsupp.comapDomain.addMonoidHom _ _ _ _ ((Â· + Â·) g) (add_right_injective g) x
#align add_monoid_algebra.div_of AddMonoidAlgebra.divOf

local infixl:70 " /áµ’á¶  " => divOf

@[simp]
theorem divOf_apply (g : G) (x : AddMonoidAlgebra k G) (g' : G) : (x /áµ’á¶  g) g' = x (g + g') :=
  rfl
#align add_monoid_algebra.div_of_apply AddMonoidAlgebra.divOf_apply

@[simp]
theorem support_divOf (g : G) (x : AddMonoidAlgebra k G) :
    (x /áµ’á¶  g).support =
      x.support.preimage ((Â· + Â·) g) (Function.Injective.injOn (add_right_injective g) _) :=
  rfl
#align add_monoid_algebra.support_div_of AddMonoidAlgebra.support_divOf

@[simp]
theorem zero_divOf (g : G) : (0 : AddMonoidAlgebra k G) /áµ’á¶  g = 0 :=
  map_zero _
#align add_monoid_algebra.zero_div_of AddMonoidAlgebra.zero_divOf

@[simp]
theorem divOf_zero (x : AddMonoidAlgebra k G) : x /áµ’á¶  0 = x := by
  refine Finsupp.ext fun _ => ?_  -- porting note: `ext` doesn't work
  -- âŠ¢ â†‘(x /áµ’á¶  0) xâœ = â†‘x xâœ
  simp only [AddMonoidAlgebra.divOf_apply, zero_add]
  -- ğŸ‰ no goals
#align add_monoid_algebra.div_of_zero AddMonoidAlgebra.divOf_zero

theorem add_divOf (x y : AddMonoidAlgebra k G) (g : G) : (x + y) /áµ’á¶  g = x /áµ’á¶  g + y /áµ’á¶  g :=
  map_add _ _ _
#align add_monoid_algebra.add_div_of AddMonoidAlgebra.add_divOf

theorem divOf_add (x : AddMonoidAlgebra k G) (a b : G) : x /áµ’á¶  (a + b) = x /áµ’á¶  a /áµ’á¶  b := by
  refine Finsupp.ext fun _ => ?_  -- porting note: `ext` doesn't work
  -- âŠ¢ â†‘(x /áµ’á¶  (a + b)) xâœ = â†‘(x /áµ’á¶  a /áµ’á¶  b) xâœ
  simp only [AddMonoidAlgebra.divOf_apply, add_assoc]
  -- ğŸ‰ no goals
#align add_monoid_algebra.div_of_add AddMonoidAlgebra.divOf_add

/-- A bundled version of `AddMonoidAlgebra.divOf`. -/
@[simps]
noncomputable def divOfHom : Multiplicative G â†’* AddMonoid.End (AddMonoidAlgebra k G) where
  toFun g :=
    { toFun := fun x => divOf x (Multiplicative.toAdd g)
      map_zero' := zero_divOf _
      map_add' := fun x y => add_divOf x y (Multiplicative.toAdd g) }
  map_one' := AddMonoidHom.ext divOf_zero
  map_mul' gâ‚ gâ‚‚ :=
    AddMonoidHom.ext fun _x =>
      (congr_arg _ (add_comm (Multiplicative.toAdd gâ‚) (Multiplicative.toAdd gâ‚‚))).trans
        (divOf_add _ _ _)
#align add_monoid_algebra.div_of_hom AddMonoidAlgebra.divOfHom

theorem of'_mul_divOf (a : G) (x : AddMonoidAlgebra k G) : of' k G a * x /áµ’á¶  a = x := by
  refine Finsupp.ext fun _ => ?_  -- porting note: `ext` doesn't work
  -- âŠ¢ â†‘(of' k G a * x /áµ’á¶  a) xâœ = â†‘x xâœ
  rw [AddMonoidAlgebra.divOf_apply, of'_apply, single_mul_apply_aux, one_mul]
  -- âŠ¢ âˆ€ (a_1 : G), a + a_1 = a + xâœ â†” a_1 = xâœ
  intro c
  -- âŠ¢ a + c = a + xâœ â†” c = xâœ
  exact add_right_inj _
  -- ğŸ‰ no goals
#align add_monoid_algebra.of'_mul_div_of AddMonoidAlgebra.of'_mul_divOf

theorem mul_of'_divOf (x : AddMonoidAlgebra k G) (a : G) : x * of' k G a /áµ’á¶  a = x := by
  refine Finsupp.ext fun _ => ?_  -- porting note: `ext` doesn't work
  -- âŠ¢ â†‘(x * of' k G a /áµ’á¶  a) xâœ = â†‘x xâœ
  rw [AddMonoidAlgebra.divOf_apply, of'_apply, mul_single_apply_aux, mul_one]
  -- âŠ¢ âˆ€ (a_1 : G), a_1 + a = a + xâœ â†” a_1 = xâœ
  intro c
  -- âŠ¢ c + a = a + xâœ â†” c = xâœ
  rw [add_comm]
  -- âŠ¢ a + c = a + xâœ â†” c = xâœ
  exact add_right_inj _
  -- ğŸ‰ no goals
#align add_monoid_algebra.mul_of'_div_of AddMonoidAlgebra.mul_of'_divOf

theorem of'_divOf (a : G) : of' k G a /áµ’á¶  a = 1 := by
  simpa only [one_mul] using mul_of'_divOf (1 : AddMonoidAlgebra k G) a
  -- ğŸ‰ no goals
#align add_monoid_algebra.of'_div_of AddMonoidAlgebra.of'_divOf

/-- The remainder upon division by `of' k G g`. -/
noncomputable def modOf (x : AddMonoidAlgebra k G) (g : G) : AddMonoidAlgebra k G :=
  x.filter fun gâ‚ => Â¬âˆƒ gâ‚‚, gâ‚ = g + gâ‚‚
#align add_monoid_algebra.mod_of AddMonoidAlgebra.modOf

local infixl:70 " %áµ’á¶  " => modOf

@[simp]
theorem modOf_apply_of_not_exists_add (x : AddMonoidAlgebra k G) (g : G) (g' : G)
    (h : Â¬âˆƒ d, g' = g + d) : (x %áµ’á¶  g) g' = x g' :=
  Finsupp.filter_apply_pos _ _ h
#align add_monoid_algebra.mod_of_apply_of_not_exists_add AddMonoidAlgebra.modOf_apply_of_not_exists_add

@[simp]
theorem modOf_apply_of_exists_add (x : AddMonoidAlgebra k G) (g : G) (g' : G)
    (h : âˆƒ d, g' = g + d) : (x %áµ’á¶  g) g' = 0 :=
  Finsupp.filter_apply_neg _ _ <| by rwa [Classical.not_not]
                                     -- ğŸ‰ no goals
#align add_monoid_algebra.mod_of_apply_of_exists_add AddMonoidAlgebra.modOf_apply_of_exists_add

@[simp]
theorem modOf_apply_add_self (x : AddMonoidAlgebra k G) (g : G) (d : G) : (x %áµ’á¶  g) (d + g) = 0 :=
  modOf_apply_of_exists_add _ _ _ âŸ¨_, add_comm _ _âŸ©
#align add_monoid_algebra.mod_of_apply_add_self AddMonoidAlgebra.modOf_apply_add_self

-- @[simp] -- Porting note: simp can prove this
theorem modOf_apply_self_add (x : AddMonoidAlgebra k G) (g : G) (d : G) : (x %áµ’á¶  g) (g + d) = 0 :=
  modOf_apply_of_exists_add _ _ _ âŸ¨_, rflâŸ©
#align add_monoid_algebra.mod_of_apply_self_add AddMonoidAlgebra.modOf_apply_self_add

theorem of'_mul_modOf (g : G) (x : AddMonoidAlgebra k G) : of' k G g * x %áµ’á¶  g = 0 := by
  refine Finsupp.ext fun g' => ?_  -- porting note: `ext g'` doesn't work
  -- âŠ¢ â†‘(of' k G g * x %áµ’á¶  g) g' = â†‘0 g'
  rw [Finsupp.zero_apply]
  -- âŠ¢ â†‘(of' k G g * x %áµ’á¶  g) g' = 0
  obtain âŸ¨d, rflâŸ© | h := em (âˆƒ d, g' = g + d)
  -- âŠ¢ â†‘(of' k G g * x %áµ’á¶  g) (g + d) = 0
  Â· rw [modOf_apply_self_add]
    -- ğŸ‰ no goals
  Â· rw [modOf_apply_of_not_exists_add _ _ _ h, of'_apply, single_mul_apply_of_not_exists_add _ _ h]
    -- ğŸ‰ no goals
#align add_monoid_algebra.of'_mul_mod_of AddMonoidAlgebra.of'_mul_modOf

theorem mul_of'_modOf (x : AddMonoidAlgebra k G) (g : G) : x * of' k G g %áµ’á¶  g = 0 := by
  refine Finsupp.ext fun g' => ?_  -- porting note: `ext g'` doesn't work
  -- âŠ¢ â†‘(x * of' k G g %áµ’á¶  g) g' = â†‘0 g'
  rw [Finsupp.zero_apply]
  -- âŠ¢ â†‘(x * of' k G g %áµ’á¶  g) g' = 0
  obtain âŸ¨d, rflâŸ© | h := em (âˆƒ d, g' = g + d)
  -- âŠ¢ â†‘(x * of' k G g %áµ’á¶  g) (g + d) = 0
  Â· rw [modOf_apply_self_add]
    -- ğŸ‰ no goals
  Â· rw [modOf_apply_of_not_exists_add _ _ _ h, of'_apply, mul_single_apply_of_not_exists_add]
    -- âŠ¢ Â¬âˆƒ d, g' = d + g
    simpa only [add_comm] using h
    -- ğŸ‰ no goals
#align add_monoid_algebra.mul_of'_mod_of AddMonoidAlgebra.mul_of'_modOf

theorem of'_modOf (g : G) : of' k G g %áµ’á¶  g = 0 := by
  simpa only [one_mul] using mul_of'_modOf (1 : AddMonoidAlgebra k G) g
  -- ğŸ‰ no goals
#align add_monoid_algebra.of'_mod_of AddMonoidAlgebra.of'_modOf

theorem divOf_add_modOf (x : AddMonoidAlgebra k G) (g : G) :
    of' k G g * (x /áµ’á¶  g) + x %áµ’á¶  g = x := by
  refine Finsupp.ext fun g' => ?_  -- porting note: `ext` doesn't work
  -- âŠ¢ â†‘(of' k G g * (x /áµ’á¶  g) + x %áµ’á¶  g) g' = â†‘x g'
  rw [Finsupp.add_apply] -- porting note: changed from `simp_rw` which can't see through the type
  -- âŠ¢ â†‘(of' k G g * (x /áµ’á¶  g)) g' + â†‘(x %áµ’á¶  g) g' = â†‘x g'
  obtain âŸ¨d, rflâŸ© | h := em (âˆƒ d, g' = g + d)
  -- âŠ¢ â†‘(of' k G g * (x /áµ’á¶  g)) (g + d) + â†‘(x %áµ’á¶  g) (g + d) = â†‘x (g + d)
  swap
  -- âŠ¢ â†‘(of' k G g * (x /áµ’á¶  g)) g' + â†‘(x %áµ’á¶  g) g' = â†‘x g'
  Â· rw [modOf_apply_of_not_exists_add x _ _ h, of'_apply, single_mul_apply_of_not_exists_add _ _ h,
      zero_add]
  Â· rw [modOf_apply_self_add, add_zero]
    -- âŠ¢ â†‘(of' k G g * (x /áµ’á¶  g)) (g + d) = â†‘x (g + d)
    rw [of'_apply, single_mul_apply_aux _ _ _, one_mul, divOf_apply]
    -- âŠ¢ âˆ€ (a : G), g + a = g + d â†” a = d
    intro a
    -- âŠ¢ g + a = g + d â†” a = d
    exact add_right_inj _
    -- ğŸ‰ no goals
#align add_monoid_algebra.div_of_add_mod_of AddMonoidAlgebra.divOf_add_modOf

theorem modOf_add_divOf (x : AddMonoidAlgebra k G) (g : G) : x %áµ’á¶  g + of' k G g * (x /áµ’á¶  g) = x :=
  by rw [add_comm, divOf_add_modOf]
     -- ğŸ‰ no goals
#align add_monoid_algebra.mod_of_add_div_of AddMonoidAlgebra.modOf_add_divOf

theorem of'_dvd_iff_modOf_eq_zero {x : AddMonoidAlgebra k G} {g : G} :
    of' k G g âˆ£ x â†” x %áµ’á¶  g = 0 := by
  constructor
  -- âŠ¢ of' k G g âˆ£ x â†’ x %áµ’á¶  g = 0
  Â· rintro âŸ¨x, rflâŸ©
    -- âŠ¢ of' k G g * x %áµ’á¶  g = 0
    rw [of'_mul_modOf]
    -- ğŸ‰ no goals
  Â· intro h
    -- âŠ¢ of' k G g âˆ£ x
    rw [â† divOf_add_modOf x g, h, add_zero]
    -- âŠ¢ of' k G g âˆ£ of' k G g * (x /áµ’á¶  g)
    exact dvd_mul_right _ _
    -- ğŸ‰ no goals
#align add_monoid_algebra.of'_dvd_iff_mod_of_eq_zero AddMonoidAlgebra.of'_dvd_iff_modOf_eq_zero

end

end AddMonoidAlgebra
