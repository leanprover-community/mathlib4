/-
Copyright (c) 2021 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.Algebra.DirectSum.Algebra
import Mathlib.Algebra.MonoidAlgebra.Basic
import Mathlib.Data.Finsupp.ToDFinsupp

#align_import algebra.monoid_algebra.to_direct_sum from "leanprover-community/mathlib"@"c0a51cf2de54089d69301befc4c73bbc2f5c7342"

/-!
# Conversion between `AddMonoidAlgebra` and homogenous `DirectSum`

This module provides conversions between `AddMonoidAlgebra` and `DirectSum`.
The latter is essentially a dependent version of the former.

Note that since `direct_sum.has_mul` combines indices additively, there is no equivalent to
`MonoidAlgebra`.

## Main definitions

* `AddMonoidAlgebra.toDirectSum : AddMonoidAlgebra M Î¹ â†’ (â¨ i : Î¹, M)`
* `DirectSum.toAddMonoidAlgebra : (â¨ i : Î¹, M) â†’ AddMonoidAlgebra M Î¹`
* Bundled equiv versions of the above:
  * `addMonoidAlgebraEquivDirectSum : AddMonoidAlgebra M Î¹ â‰ƒ (â¨ i : Î¹, M)`
  * `addMonoidAlgebraAddEquivDirectSum : AddMonoidAlgebra M Î¹ â‰ƒ+ (â¨ i : Î¹, M)`
  * `addMonoidAlgebraRingEquivDirectSum R : AddMonoidAlgebra M Î¹ â‰ƒ+* (â¨ i : Î¹, M)`
  * `addMonoidAlgebraAlgEquivDirectSum R : AddMonoidAlgebra A Î¹ â‰ƒâ‚[R] (â¨ i : Î¹, A)`

## Theorems

The defining feature of these operations is that they map `Finsupp.single` to
`DirectSum.of` and vice versa:

* `AddMonoidAlgebra.toDirectSum_single`
* `DirectSum.toAddMonoidAlgebra_of`

as well as preserving arithmetic operations.

For the bundled equivalences, we provide lemmas that they reduce to
`AddMonoidAlgebra.toDirectSum`:

* `addMonoidAlgebraAddEquivDirectSum_apply`
* `add_monoid_algebra_lequiv_direct_sum_apply`
* `addMonoidAlgebraAddEquivDirectSum_symm_apply`
* `add_monoid_algebra_lequiv_direct_sum_symm_apply`

## Implementation notes

This file largely just copies the API of `Mathlib/Data/Finsupp/ToDFinsupp.lean`, and reuses the
proofs. Recall that `AddMonoidAlgebra M Î¹` is defeq to `Î¹ â†’â‚€ M` and `â¨ i : Î¹, M` is defeq to
`Î â‚€ i : Î¹, M`.

Note that there is no `AddMonoidAlgebra` equivalent to `Finsupp.single`, so many statements
still involve this definition.
-/


variable {Î¹ : Type*} {R : Type*} {M : Type*} {A : Type*}

open DirectSum

/-! ### Basic definitions and lemmas -/


section Defs

/-- Interpret an `AddMonoidAlgebra` as a homogenous `DirectSum`. -/
def AddMonoidAlgebra.toDirectSum [Semiring M] (f : AddMonoidAlgebra M Î¹) : â¨ _ : Î¹, M :=
  Finsupp.toDFinsupp f
#align add_monoid_algebra.to_direct_sum AddMonoidAlgebra.toDirectSum

section

variable [DecidableEq Î¹] [Semiring M]

@[simp]
theorem AddMonoidAlgebra.toDirectSum_single (i : Î¹) (m : M) :
    AddMonoidAlgebra.toDirectSum (Finsupp.single i m) = DirectSum.of _ i m :=
  Finsupp.toDFinsupp_single i m
#align add_monoid_algebra.to_direct_sum_single AddMonoidAlgebra.toDirectSum_single

variable [âˆ€ m : M, Decidable (m â‰  0)]

/-- Interpret a homogenous `DirectSum` as an `AddMonoidAlgebra`. -/
def DirectSum.toAddMonoidAlgebra (f : â¨ _ : Î¹, M) : AddMonoidAlgebra M Î¹ :=
  DFinsupp.toFinsupp f
#align direct_sum.to_add_monoid_algebra DirectSum.toAddMonoidAlgebra

@[simp]
theorem DirectSum.toAddMonoidAlgebra_of (i : Î¹) (m : M) :
    (DirectSum.of _ i m : â¨ _ : Î¹, M).toAddMonoidAlgebra = Finsupp.single i m :=
  DFinsupp.toFinsupp_single i m
#align direct_sum.to_add_monoid_algebra_of DirectSum.toAddMonoidAlgebra_of

@[simp]
theorem AddMonoidAlgebra.toDirectSum_toAddMonoidAlgebra (f : AddMonoidAlgebra M Î¹) :
    f.toDirectSum.toAddMonoidAlgebra = f :=
  Finsupp.toDFinsupp_toFinsupp f
#align add_monoid_algebra.to_direct_sum_to_add_monoid_algebra AddMonoidAlgebra.toDirectSum_toAddMonoidAlgebra

@[simp]
theorem DirectSum.toAddMonoidAlgebra_toDirectSum (f : â¨ _ : Î¹, M) :
    f.toAddMonoidAlgebra.toDirectSum = f :=
  (DFinsupp.toFinsupp_toDFinsupp (show Î â‚€ _ : Î¹, M from f) : _)
#align direct_sum.to_add_monoid_algebra_to_direct_sum DirectSum.toAddMonoidAlgebra_toDirectSum

end

end Defs

/-! ### Lemmas about arithmetic operations -/


section Lemmas

namespace AddMonoidAlgebra

@[simp]
theorem toDirectSum_zero [Semiring M] : (0 : AddMonoidAlgebra M Î¹).toDirectSum = 0 :=
  Finsupp.toDFinsupp_zero
#align add_monoid_algebra.to_direct_sum_zero AddMonoidAlgebra.toDirectSum_zero

@[simp]
theorem toDirectSum_add [Semiring M] (f g : AddMonoidAlgebra M Î¹) :
    (f + g).toDirectSum = f.toDirectSum + g.toDirectSum :=
  Finsupp.toDFinsupp_add _ _
#align add_monoid_algebra.to_direct_sum_add AddMonoidAlgebra.toDirectSum_add

@[simp]
theorem toDirectSum_mul [DecidableEq Î¹] [AddMonoid Î¹] [Semiring M] (f g : AddMonoidAlgebra M Î¹) :
    (f * g).toDirectSum = f.toDirectSum * g.toDirectSum := by
  let to_hom : AddMonoidAlgebra M Î¹ â†’+ â¨ _ : Î¹, M :=
  { toFun := toDirectSum
    map_zero' := toDirectSum_zero
    map_add' := toDirectSum_add }
  show to_hom (f * g) = to_hom f * to_hom g
  -- âŠ¢ â†‘to_hom (f * g) = â†‘to_hom f * â†‘to_hom g
  let _ : NonUnitalNonAssocSemiring (Î¹ â†’â‚€ M) := AddMonoidAlgebra.nonUnitalNonAssocSemiring
  -- âŠ¢ â†‘to_hom (f * g) = â†‘to_hom f * â†‘to_hom g
  revert f g
  -- âŠ¢ âˆ€ (f g : AddMonoidAlgebra M Î¹), â†‘to_hom (f * g) = â†‘to_hom f * â†‘to_hom g
  rw [AddMonoidHom.map_mul_iff]
  -- âŠ¢ AddMonoidHom.comprâ‚‚ AddMonoidHom.mul to_hom = AddMonoidHom.complâ‚‚ (AddMonoid â€¦
  -- porting note: does not find `addHom_ext'`, was `ext (xi xv yi yv) : 4`
  refine Finsupp.addHom_ext' fun xi => AddMonoidHom.ext fun xv => ?_
  -- âŠ¢ â†‘(AddMonoidHom.comp (AddMonoidHom.comprâ‚‚ AddMonoidHom.mul to_hom) (Finsupp.s â€¦
  refine Finsupp.addHom_ext' fun yi => AddMonoidHom.ext fun yv => ?_
  -- âŠ¢ â†‘(AddMonoidHom.comp (â†‘(AddMonoidHom.comp (AddMonoidHom.comprâ‚‚ AddMonoidHom.m â€¦
  dsimp only [AddMonoidHom.comp_apply, AddMonoidHom.complâ‚‚_apply, AddMonoidHom.comprâ‚‚_apply,
    AddMonoidHom.mul_apply, Finsupp.singleAddHom_apply]
  simp only [AddMonoidHom.coe_mk, ZeroHom.coe_mk, toDirectSum_single]
  -- âŠ¢ toDirectSum (Finsupp.single xi xv * Finsupp.single yi yv) = â†‘(DirectSum.of ( â€¦
  erw [AddMonoidAlgebra.single_mul_single, AddMonoidHom.coe_mk, ZeroHom.coe_mk,
    AddMonoidAlgebra.toDirectSum_single]
  simp only [AddMonoidHom.coe_comp, AddMonoidHom.coe_mul, AddMonoidHom.coe_mk, ZeroHom.coe_mk,
    Function.comp_apply, toDirectSum_single, AddMonoidHom.id_apply, Finsupp.singleAddHom_apply,
    AddMonoidHom.coe_mulLeft]
  erw [DirectSum.of_mul_of, Mul.gMul_mul]
  -- ðŸŽ‰ no goals
#align add_monoid_algebra.to_direct_sum_mul AddMonoidAlgebra.toDirectSum_mul

end AddMonoidAlgebra

namespace DirectSum

variable [DecidableEq Î¹]

@[simp]
theorem toAddMonoidAlgebra_zero [Semiring M] [âˆ€ m : M, Decidable (m â‰  0)] :
    toAddMonoidAlgebra 0 = (0 : AddMonoidAlgebra M Î¹) :=
  DFinsupp.toFinsupp_zero
#align direct_sum.to_add_monoid_algebra_zero DirectSum.toAddMonoidAlgebra_zero

@[simp]
theorem toAddMonoidAlgebra_add [Semiring M] [âˆ€ m : M, Decidable (m â‰  0)] (f g : â¨ _ : Î¹, M) :
    (f + g).toAddMonoidAlgebra = toAddMonoidAlgebra f + toAddMonoidAlgebra g :=
  DFinsupp.toFinsupp_add _ _
#align direct_sum.to_add_monoid_algebra_add DirectSum.toAddMonoidAlgebra_add

@[simp]
theorem toAddMonoidAlgebra_mul [AddMonoid Î¹] [Semiring M]
  [âˆ€ m : M, Decidable (m â‰  0)] (f g : â¨ _ : Î¹, M) :
      (f * g).toAddMonoidAlgebra = toAddMonoidAlgebra f * toAddMonoidAlgebra g := by
  apply_fun AddMonoidAlgebra.toDirectSum
  -- âŠ¢ AddMonoidAlgebra.toDirectSum (toAddMonoidAlgebra (f * g)) = AddMonoidAlgebra â€¦
  Â· simp
    -- ðŸŽ‰ no goals
  Â· apply Function.LeftInverse.injective
    -- âŠ¢ Function.LeftInverse ?inj.g AddMonoidAlgebra.toDirectSum
    apply AddMonoidAlgebra.toDirectSum_toAddMonoidAlgebra
    -- ðŸŽ‰ no goals
#align direct_sum.to_add_monoid_algebra_mul DirectSum.toAddMonoidAlgebra_mul

end DirectSum

end Lemmas

/-! ### Bundled `Equiv`s -/


section Equivs

/-- `AddMonoidAlgebra.toDirectSum` and `DirectSum.toAddMonoidAlgebra` together form an
equiv. -/
@[simps (config := { fullyApplied := false })]
def addMonoidAlgebraEquivDirectSum [DecidableEq Î¹] [Semiring M] [âˆ€ m : M, Decidable (m â‰  0)] :
    AddMonoidAlgebra M Î¹ â‰ƒ â¨ _ : Î¹, M :=
  { finsuppEquivDFinsupp with
    toFun := AddMonoidAlgebra.toDirectSum
    invFun := DirectSum.toAddMonoidAlgebra }
#align add_monoid_algebra_equiv_direct_sum addMonoidAlgebraEquivDirectSum

/-- The additive version of `AddMonoidAlgebra.addMonoidAlgebraEquivDirectSum`.  -/
@[simps (config := { fullyApplied := false })]
def addMonoidAlgebraAddEquivDirectSum [DecidableEq Î¹] [Semiring M] [âˆ€ m : M, Decidable (m â‰  0)] :
    AddMonoidAlgebra M Î¹ â‰ƒ+ â¨ _ : Î¹, M :=
  { addMonoidAlgebraEquivDirectSum with
    toFun := AddMonoidAlgebra.toDirectSum
    invFun := DirectSum.toAddMonoidAlgebra
    map_add' := AddMonoidAlgebra.toDirectSum_add }
#align add_monoid_algebra_add_equiv_direct_sum addMonoidAlgebraAddEquivDirectSum

/-- The ring version of `AddMonoidAlgebra.addMonoidAlgebraEquivDirectSum`.  -/
@[simps (config := { fullyApplied := false })]
def addMonoidAlgebraRingEquivDirectSum [DecidableEq Î¹] [AddMonoid Î¹] [Semiring M]
    [âˆ€ m : M, Decidable (m â‰  0)] : AddMonoidAlgebra M Î¹ â‰ƒ+* â¨ _ : Î¹, M :=
  { (addMonoidAlgebraAddEquivDirectSum : AddMonoidAlgebra M Î¹ â‰ƒ+ â¨ _ : Î¹, M) with
    toFun := AddMonoidAlgebra.toDirectSum
    invFun := DirectSum.toAddMonoidAlgebra
    map_mul' := AddMonoidAlgebra.toDirectSum_mul }
#align add_monoid_algebra_ring_equiv_direct_sum addMonoidAlgebraRingEquivDirectSum

/-- The algebra version of `AddMonoidAlgebra.addMonoidAlgebraEquivDirectSum`. -/
@[simps (config := { fullyApplied := false })]
def addMonoidAlgebraAlgEquivDirectSum [DecidableEq Î¹] [AddMonoid Î¹] [CommSemiring R] [Semiring A]
    [Algebra R A] [âˆ€ m : A, Decidable (m â‰  0)] : AddMonoidAlgebra A Î¹ â‰ƒâ‚[R] â¨ _ : Î¹, A :=
  { (addMonoidAlgebraRingEquivDirectSum : AddMonoidAlgebra A Î¹ â‰ƒ+* â¨ _ : Î¹, A) with
    toFun := AddMonoidAlgebra.toDirectSum
    invFun := DirectSum.toAddMonoidAlgebra
    commutes' := fun _r => AddMonoidAlgebra.toDirectSum_single _ _ }
#align add_monoid_algebra_alg_equiv_direct_sum addMonoidAlgebraAlgEquivDirectSum

end Equivs
