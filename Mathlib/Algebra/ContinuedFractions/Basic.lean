/-
Copyright (c) 2019 Kevin Kappelmann. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Kappelmann
-/
import Mathlib.Data.Seq.Seq
import Mathlib.Algebra.Field.Defs

#align_import algebra.continued_fractions.basic from "leanprover-community/mathlib"@"a7e36e48519ab281320c4d192da6a7b348ce40ad"

/-!
# Basic Definitions/Theorems for Continued Fractions

## Summary

We define generalised, simple, and regular continued fractions and functions to evaluate their
convergents. We follow the naming conventions from Wikipedia and [wall2018analytic], Chapter 1.

## Main definitions

1. Generalised continued fractions (gcfs)
2. Simple continued fractions (scfs)
3. (Regular) continued fractions ((r)cfs)
4. Computation of convergents using the recurrence relation in `convergents`.
5. Computation of convergents by directly evaluating the fraction described by the gcf in
`convergents'`.

## Implementation notes

1. The most commonly used kind of continued fractions in the literature are regular continued
fractions. We hence just call them `ContinuedFractions` in the library.
2. We use sequences from `Data.Seq` to encode potentially infinite sequences.

## References

- <https://en.wikipedia.org/wiki/Generalized_continued_fraction>
- [Wall, H.S., *Analytic Theory of Continued Fractions*][wall2018analytic]

## Tags

numerics, number theory, approximations, fractions
-/

-- Fix a carrier `Œ±`.
variable (Œ± : Type*)

/-!### Definitions-/

-- Porting note: Originally `protected structure GeneralizedContinuedFraction.Pair`
/-- We collect a partial numerator `a·µ¢` and partial denominator `b·µ¢` in a pair `‚ü®a·µ¢, b·µ¢‚ü©`. -/
structure GeneralizedContinuedFraction.Pair where
  /-- Partial numerator -/
  a : Œ±
  /-- Partial denominator -/
  b : Œ±
  deriving Inhabited
#align generalized_continued_fraction.pair GeneralizedContinuedFraction.Pair

open GeneralizedContinuedFraction

/-! Interlude: define some expected coercions and instances. -/

namespace GeneralizedContinuedFraction.Pair

variable {Œ±}

/-- Make a `GCF.Pair` printable. -/
instance [Repr Œ±] : Repr (Pair Œ±) :=
  ‚ü®fun p _ ‚Ü¶ "(a : " ++ repr p.a ++ ", b : " ++ repr p.b ++ ")"‚ü©

/-- Maps a function `f` on both components of a given pair. -/
def map {Œ≤ : Type*} (f : Œ± ‚Üí Œ≤) (gp : Pair Œ±) : Pair Œ≤ :=
  ‚ü®f gp.a, f gp.b‚ü©
#align generalized_continued_fraction.pair.map GeneralizedContinuedFraction.Pair.map

section coe

-- Fix another type `Œ≤` which we will convert to.
variable {Œ≤ : Type*} [Coe Œ± Œ≤]

-- Porting note: added so we can add the `@[coe]` attribute
/-- The coercion between numerator-denominator pairs happens componentwise. -/
@[coe]
def coeFn : Pair Œ± ‚Üí Pair Œ≤ := map (‚Üë)

/-- Coerce a pair by elementwise coercion. -/
instance : Coe (Pair Œ±) (Pair Œ≤) :=
  ‚ü®coeFn‚ü©

@[simp, norm_cast]
theorem coe_toPair {a b : Œ±} : (‚Üë(Pair.mk a b) : Pair Œ≤) = Pair.mk (a : Œ≤) (b : Œ≤) := rfl
#align generalized_continued_fraction.pair.coe_to_generalized_continued_fraction_pair GeneralizedContinuedFraction.Pair.coe_toPair

end coe

end GeneralizedContinuedFraction.Pair

/-- A *generalised continued fraction* (gcf) is a potentially infinite expression of the form
$$
  h + \dfrac{a_0}
            {b_0 + \dfrac{a_1}
                         {b_1 + \dfrac{a_2}
                                      {b_2 + \dfrac{a_3}
                                                   {b_3 + \dots}}}}
$$
where `h` is called the *head term* or *integer part*, the `a·µ¢` are called the
*partial numerators* and the `b·µ¢` the *partial denominators* of the gcf.
We store the sequence of partial numerators and denominators in a sequence of
`GeneralizedContinuedFraction.Pair`s `s`.
For convenience, one often writes `[h; (a‚ÇÄ, b‚ÇÄ), (a‚ÇÅ, b‚ÇÅ), (a‚ÇÇ, b‚ÇÇ),...]`.
-/
@[ext]
structure GeneralizedContinuedFraction where
  /-- Head term -/
  h : Œ±
  /-- Sequence of partial numerator and denominator pairs. -/
  s : Stream'.Seq <| Pair Œ±
#align generalized_continued_fraction GeneralizedContinuedFraction
#align generalized_continued_fraction.ext_iff GeneralizedContinuedFraction.ext_iff
#align generalized_continued_fraction.ext GeneralizedContinuedFraction.ext

variable {Œ±}

namespace GeneralizedContinuedFraction

/-- Constructs a generalized continued fraction without fractional part. -/
def ofInteger (a : Œ±) : GeneralizedContinuedFraction Œ± :=
  ‚ü®a, Stream'.Seq.nil‚ü©
#align generalized_continued_fraction.of_integer GeneralizedContinuedFraction.ofInteger

instance [Inhabited Œ±] : Inhabited (GeneralizedContinuedFraction Œ±) :=
  ‚ü®ofInteger default‚ü©

/-- Returns the sequence of partial numerators `a·µ¢` of `g`. -/
def partialNumerators (g : GeneralizedContinuedFraction Œ±) : Stream'.Seq Œ± :=
  g.s.map Pair.a
#align generalized_continued_fraction.partial_numerators GeneralizedContinuedFraction.partialNumerators

/-- Returns the sequence of partial denominators `b·µ¢` of `g`. -/
def partialDenominators (g : GeneralizedContinuedFraction Œ±) : Stream'.Seq Œ± :=
  g.s.map Pair.b
#align generalized_continued_fraction.partial_denominators GeneralizedContinuedFraction.partialDenominators

/-- A gcf terminated at position `n` if its sequence terminates at position `n`. -/
def TerminatedAt (g : GeneralizedContinuedFraction Œ±) (n : ‚Ñï) : Prop :=
  g.s.TerminatedAt n
#align generalized_continued_fraction.terminated_at GeneralizedContinuedFraction.TerminatedAt

/-- It is decidable whether a gcf terminated at a given position. -/
instance terminatedAtDecidable (g : GeneralizedContinuedFraction Œ±) (n : ‚Ñï) :
    Decidable (g.TerminatedAt n) := by
  unfold TerminatedAt
  -- ‚ä¢ Decidable (Stream'.Seq.TerminatedAt g.s n)
  infer_instance
  -- üéâ no goals
#align generalized_continued_fraction.terminated_at_decidable GeneralizedContinuedFraction.terminatedAtDecidable

/-- A gcf terminates if its sequence terminates. -/
def Terminates (g : GeneralizedContinuedFraction Œ±) : Prop :=
  g.s.Terminates
#align generalized_continued_fraction.terminates GeneralizedContinuedFraction.Terminates

section coe

/-! Interlude: define some expected coercions. -/

-- Fix another type `Œ≤` which we will convert to.
variable {Œ≤ : Type*} [Coe Œ± Œ≤]

-- Porting note: Added to put `@[coe]` attr on it.
/-- The coercion between `GeneralizedContinuedFraction` happens on the head term
and all numerator-denominator pairs componentwise. -/
@[coe]
def coeFn : GeneralizedContinuedFraction Œ± ‚Üí GeneralizedContinuedFraction Œ≤ :=
  fun g ‚Ü¶ ‚ü®(g.h : Œ≤), (g.s.map (‚Üë) : Stream'.Seq <| Pair Œ≤)‚ü©

/-- Coerce a gcf by elementwise coercion. -/
instance : Coe (GeneralizedContinuedFraction Œ±) (GeneralizedContinuedFraction Œ≤) :=
  ‚ü®coeFn‚ü©

@[simp, norm_cast]
theorem coe_toGeneralizedContinuedFraction {g : GeneralizedContinuedFraction Œ±} :
    (g : GeneralizedContinuedFraction Œ≤) =
      ‚ü®(g.h : Œ≤), (g.s.map (‚Üë) : Stream'.Seq <| Pair Œ≤)‚ü© := rfl
#align generalized_continued_fraction.coe_to_generalized_continued_fraction GeneralizedContinuedFraction.coe_toGeneralizedContinuedFraction

end coe

end GeneralizedContinuedFraction

/-- A generalized continued fraction is a *simple continued fraction* if all partial numerators are
equal to one.
$$
  h + \dfrac{1}
            {b_0 + \dfrac{1}
                         {b_1 + \dfrac{1}
                                      {b_2 + \dfrac{1}
                                                   {b_3 + \dots}}}}
$$
-/
def GeneralizedContinuedFraction.IsSimpleContinuedFraction (g : GeneralizedContinuedFraction Œ±)
    [One Œ±] : Prop :=
  ‚àÄ (n : ‚Ñï) (a‚Çô : Œ±), g.partialNumerators.get? n = some a‚Çô ‚Üí a‚Çô = 1
#align generalized_continued_fraction.is_simple_continued_fraction GeneralizedContinuedFraction.IsSimpleContinuedFraction

variable (Œ±)

/-- A *simple continued fraction* (scf) is a generalized continued fraction (gcf) whose partial
numerators are equal to one.
$$
  h + \dfrac{1}
            {b_0 + \dfrac{1}
                         {b_1 + \dfrac{1}
                                      {b_2 + \dfrac{1}
                                                   {b_3 + \dots}}}}
$$
For convenience, one often writes `[h; b‚ÇÄ, b‚ÇÅ, b‚ÇÇ,...]`.
It is encoded as the subtype of gcfs that satisfy
`GeneralizedContinuedFraction.IsSimpleContinuedFraction`.
 -/
def SimpleContinuedFraction [One Œ±] :=
  { g : GeneralizedContinuedFraction Œ± // g.IsSimpleContinuedFraction }
#align simple_continued_fraction SimpleContinuedFraction

variable {Œ±}

-- Interlude: define some expected coercions.
namespace SimpleContinuedFraction

variable [One Œ±]

/-- Constructs a simple continued fraction without fractional part. -/
def ofInteger (a : Œ±) : SimpleContinuedFraction Œ± :=
  ‚ü®GeneralizedContinuedFraction.ofInteger a, fun n a‚Çô h ‚Ü¶ by cases h‚ü©
                                                             -- üéâ no goals
#align simple_continued_fraction.of_integer SimpleContinuedFraction.ofInteger

instance : Inhabited (SimpleContinuedFraction Œ±) :=
  ‚ü®ofInteger 1‚ü©

/-- Lift a scf to a gcf using the inclusion map. -/
instance : Coe (SimpleContinuedFraction Œ±) (GeneralizedContinuedFraction Œ±) :=
  -- Porting note: originally `by unfold SimpleContinuedFraction; infer_instance`
  ‚ü®Subtype.val‚ü©

-- Porting note: Syntactic tautology due to change in `Coe` above.
-- theorem coe_toGeneralizedContinuedFraction {s : SimpleContinuedFraction Œ±} :
--     (‚Üës : GeneralizedContinuedFraction Œ±) = s.val := rfl
#noalign simple_continued_fraction.coe_to_generalized_continued_fraction

end SimpleContinuedFraction

/--
A simple continued fraction is a *(regular) continued fraction* ((r)cf) if all partial denominators
`b·µ¢` are positive, i.e. `0 < b·µ¢`.
-/
def SimpleContinuedFraction.IsContinuedFraction [One Œ±] [Zero Œ±] [LT Œ±]
    (s : SimpleContinuedFraction Œ±) : Prop :=
  ‚àÄ (n : ‚Ñï) (b‚Çô : Œ±),
    (‚Üës : GeneralizedContinuedFraction Œ±).partialDenominators.get? n = some b‚Çô ‚Üí 0 < b‚Çô
#align simple_continued_fraction.is_continued_fraction SimpleContinuedFraction.IsContinuedFraction

variable (Œ±)

/-- A *(regular) continued fraction* ((r)cf) is a simple continued fraction (scf) whose partial
denominators are all positive. It is the subtype of scfs that satisfy
`SimpleContinuedFraction.IsContinuedFraction`.
 -/
def ContinuedFraction [One Œ±] [Zero Œ±] [LT Œ±] :=
  { s : SimpleContinuedFraction Œ± // s.IsContinuedFraction }
#align continued_fraction ContinuedFraction

variable {Œ±}

/-! Interlude: define some expected coercions. -/

namespace ContinuedFraction

variable [One Œ±] [Zero Œ±] [LT Œ±]

/-- Constructs a continued fraction without fractional part. -/
def ofInteger (a : Œ±) : ContinuedFraction Œ± :=
  ‚ü®SimpleContinuedFraction.ofInteger a, fun n b‚Çô h ‚Ü¶ by cases h‚ü©
                                                        -- üéâ no goals
#align continued_fraction.of_integer ContinuedFraction.ofInteger

instance : Inhabited (ContinuedFraction Œ±) :=
  ‚ü®ofInteger 0‚ü©

/-- Lift a cf to a scf using the inclusion map. -/
instance : Coe (ContinuedFraction Œ±) (SimpleContinuedFraction Œ±) :=
  -- Porting note: originally `by unfold ContinuedFraction; infer_instance`
  ‚ü®Subtype.val‚ü©

-- Porting note: Syntactic tautology due to change of `Coe` above.
-- theorem coe_to_simpleContinuedFraction {c : ContinuedFraction Œ±} :
--     (‚Üëc : SimpleContinuedFraction Œ±) = c.val := rfl
#noalign continued_fraction.coe_to_simple_continued_fraction

/-- Lift a cf to a scf using the inclusion map. -/
instance : Coe (ContinuedFraction Œ±) (GeneralizedContinuedFraction Œ±) :=
  ‚ü®fun c ‚Ü¶ c.val‚ü©
  -- Porting note: was `fun c ‚Ü¶ ‚Üë(‚Üëc : SimpleContinuedFraction Œ±)`

-- Porting note: Syntactic tautology due to change of `Coe` above.
-- theorem coe_toGeneralizedContinuedFraction {c : ContinuedFraction Œ±} :
--     (‚Üëc : GeneralizedContinuedFraction Œ±) = c.val := rfl
#noalign continued_fraction.coe_to_generalized_continued_fraction

end ContinuedFraction

namespace GeneralizedContinuedFraction

/-!
### Computation of Convergents

We now define how to compute the convergents of a gcf. There are two standard ways to do this:
directly evaluating the (infinite) fraction described by the gcf or using a recurrence relation.
For (r)cfs, these computations are equivalent as shown in
`Algebra.ContinuedFractions.ConvergentsEquiv`.
-/

-- Fix a division ring for the computations.
variable {K : Type*} [DivisionRing K]

/-!
We start with the definition of the recurrence relation. Given a gcf `g`, for all `n ‚â• 1`, we define
- `A‚Çã‚ÇÅ = 1,  A‚ÇÄ = h,  A‚Çô = b‚Çô‚Çã‚ÇÅ * A‚Çô‚Çã‚ÇÅ + a‚Çô‚Çã‚ÇÅ * A‚Çô‚Çã‚ÇÇ`, and
- `B‚Çã‚ÇÅ = 0,  B‚ÇÄ = 1,  B‚Çô = b‚Çô‚Çã‚ÇÅ * B‚Çô‚Çã‚ÇÅ + a‚Çô‚Çã‚ÇÅ * B‚Çô‚Çã‚ÇÇ`.

`A‚Çô, B‚Çô` are called the *nth continuants*, `A‚Çô` the *nth numerator*, and `B‚Çô` the
*nth denominator* of `g`. The *nth convergent* of `g` is given by `A‚Çô / B‚Çô`.
-/

/-- Returns the next numerator `A‚Çô = b‚Çô‚Çã‚ÇÅ * A‚Çô‚Çã‚ÇÅ + a‚Çô‚Çã‚ÇÅ * A‚Çô‚Çã‚ÇÇ`, where `predA` is `A‚Çô‚Çã‚ÇÅ`,
`ppredA` is `A‚Çô‚Çã‚ÇÇ`, `a` is `a‚Çô‚Çã‚ÇÅ`, and `b` is `b‚Çô‚Çã‚ÇÅ`.
-/
def nextNumerator (a b ppredA predA : K) : K :=
  b * predA + a * ppredA
#align generalized_continued_fraction.next_numerator GeneralizedContinuedFraction.nextNumerator

/-- Returns the next denominator `B‚Çô = b‚Çô‚Çã‚ÇÅ * B‚Çô‚Çã‚ÇÅ + a‚Çô‚Çã‚ÇÅ * B‚Çô‚Çã‚ÇÇ`, where `predB` is `B‚Çô‚Çã‚ÇÅ` and
`ppredB` is `B‚Çô‚Çã‚ÇÇ`, `a` is `a‚Çô‚Çã‚ÇÅ`, and `b` is `b‚Çô‚Çã‚ÇÅ`.
-/
def nextDenominator (a‚Çô b‚Çô ppredB predB : K) : K :=
  b‚Çô * predB + a‚Çô * ppredB
#align generalized_continued_fraction.next_denominator GeneralizedContinuedFraction.nextDenominator

/--
Returns the next continuants `‚ü®A‚Çô, B‚Çô‚ü©` using `nextNumerator` and `nextDenominator`, where `pred`
is `‚ü®A‚Çô‚Çã‚ÇÅ, B‚Çô‚Çã‚ÇÅ‚ü©`, `ppred` is `‚ü®A‚Çô‚Çã‚ÇÇ, B‚Çô‚Çã‚ÇÇ‚ü©`, `a` is `a‚Çô‚Çã‚ÇÅ`, and `b` is `b‚Çô‚Çã‚ÇÅ`.
-/
def nextContinuants (a b : K) (ppred pred : Pair K) : Pair K :=
  ‚ü®nextNumerator a b ppred.a pred.a, nextDenominator a b ppred.b pred.b‚ü©
#align generalized_continued_fraction.next_continuants GeneralizedContinuedFraction.nextContinuants

/-- Returns the continuants `‚ü®A‚Çô‚Çã‚ÇÅ, B‚Çô‚Çã‚ÇÅ‚ü©` of `g`. -/
def continuantsAux (g : GeneralizedContinuedFraction K) : Stream' (Pair K)
  | 0 => ‚ü®1, 0‚ü©
  | 1 => ‚ü®g.h, 1‚ü©
  | n + 2 =>
    match g.s.get? n with
    | none => continuantsAux g (n + 1)
    | some gp => nextContinuants gp.a gp.b (continuantsAux g n) (continuantsAux g (n + 1))
#align generalized_continued_fraction.continuants_aux GeneralizedContinuedFraction.continuantsAux

/-- Returns the continuants `‚ü®A‚Çô, B‚Çô‚ü©` of `g`. -/
def continuants (g : GeneralizedContinuedFraction K) : Stream' (Pair K) :=
  g.continuantsAux.tail
#align generalized_continued_fraction.continuants GeneralizedContinuedFraction.continuants

/-- Returns the numerators `A‚Çô` of `g`. -/
def numerators (g : GeneralizedContinuedFraction K) : Stream' K :=
  g.continuants.map Pair.a
#align generalized_continued_fraction.numerators GeneralizedContinuedFraction.numerators

/-- Returns the denominators `B‚Çô` of `g`. -/
def denominators (g : GeneralizedContinuedFraction K) : Stream' K :=
  g.continuants.map Pair.b
#align generalized_continued_fraction.denominators GeneralizedContinuedFraction.denominators

/-- Returns the convergents `A‚Çô / B‚Çô` of `g`, where `A‚Çô, B‚Çô` are the nth continuants of `g`. -/
def convergents (g : GeneralizedContinuedFraction K) : Stream' K :=
  fun n : ‚Ñï ‚Ü¶ g.numerators n / g.denominators n
#align generalized_continued_fraction.convergents GeneralizedContinuedFraction.convergents

/--
Returns the approximation of the fraction described by the given sequence up to a given position n.
For example, `convergents'Aux [(1, 2), (3, 4), (5, 6)] 2 = 1 / (2 + 3 / 4)` and
`convergents'Aux [(1, 2), (3, 4), (5, 6)] 0 = 0`.
-/
def convergents'Aux : Stream'.Seq (Pair K) ‚Üí ‚Ñï ‚Üí K
  | _, 0 => 0
  | s, n + 1 =>
    match s.head with
    | none => 0
    | some gp => gp.a / (gp.b + convergents'Aux s.tail n)
#align generalized_continued_fraction.convergents'_aux GeneralizedContinuedFraction.convergents'Aux

/-- Returns the convergents of `g` by evaluating the fraction described by `g` up to a given
position `n`. For example, `convergents' [9; (1, 2), (3, 4), (5, 6)] 2 = 9 + 1 / (2 + 3 / 4)` and
`convergents' [9; (1, 2), (3, 4), (5, 6)] 0 = 9`
-/
def convergents' (g : GeneralizedContinuedFraction K) (n : ‚Ñï) : K :=
  g.h + convergents'Aux g.s n
#align generalized_continued_fraction.convergents' GeneralizedContinuedFraction.convergents'

end GeneralizedContinuedFraction
