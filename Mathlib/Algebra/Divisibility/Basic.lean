/-
Copyright (c) 2014 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Leonardo de Moura, Floris van Doorn, Amelia Livingston, Yury Kudryashov,
Neil Strickland, Aaron Anderson
-/
import Mathlib.Algebra.Hom.Group

#align_import algebra.divisibility.basic from "leanprover-community/mathlib"@"e8638a0fcaf73e4500469f368ef9494e495099b3"

/-!
# Divisibility

This file defines the basics of the divisibility relation in the context of `(Comm)` `Monoid`s.

## Main definitions

 * `semigroupDvd`

## Implementation notes

The divisibility relation is defined for all monoids, and as such, depends on the order of
  multiplication if the monoid is not commutative. There are two possible conventions for
  divisibility in the noncommutative context, and this relation follows the convention for ordinals,
  so `a | b` is defined as `‚àÉ c, b = a * c`.

## Tags

divisibility, divides
-/


variable {Œ± : Type*}

section Semigroup

variable [Semigroup Œ±] {a b c : Œ±}

/-- There are two possible conventions for divisibility, which coincide in a `CommMonoid`.
    This matches the convention for ordinals. -/
instance (priority := 100) semigroupDvd : Dvd Œ± :=
  Dvd.mk fun a b => ‚àÉ c, b = a * c
#align semigroup_has_dvd semigroupDvd

-- TODO: this used to not have `c` explicit, but that seems to be important
--       for use with tactics, similar to `Exists.intro`
theorem Dvd.intro (c : Œ±) (h : a * c = b) : a ‚à£ b :=
  Exists.intro c h.symm
#align dvd.intro Dvd.intro

alias dvd_of_mul_right_eq := Dvd.intro
#align dvd_of_mul_right_eq dvd_of_mul_right_eq

theorem exists_eq_mul_right_of_dvd (h : a ‚à£ b) : ‚àÉ c, b = a * c :=
  h
#align exists_eq_mul_right_of_dvd exists_eq_mul_right_of_dvd

theorem dvd_def : a ‚à£ b ‚Üî ‚àÉ c, b = a * c :=
  Iff.rfl

alias dvd_iff_exists_eq_mul_right := dvd_def

theorem Dvd.elim {P : Prop} {a b : Œ±} (H‚ÇÅ : a ‚à£ b) (H‚ÇÇ : ‚àÄ c, b = a * c ‚Üí P) : P :=
  Exists.elim H‚ÇÅ H‚ÇÇ
#align dvd.elim Dvd.elim

attribute [local simp] mul_assoc mul_comm mul_left_comm

@[trans]
theorem dvd_trans : a ‚à£ b ‚Üí b ‚à£ c ‚Üí a ‚à£ c
  | ‚ü®d, h‚ÇÅ‚ü©, ‚ü®e, h‚ÇÇ‚ü© => ‚ü®d * e, h‚ÇÅ ‚ñ∏ h‚ÇÇ.trans <| mul_assoc a d e‚ü©
#align dvd_trans dvd_trans

alias Dvd.dvd.trans := dvd_trans

/-- Transitivity of `|` for use in `calc` blocks. -/
instance : IsTrans Œ± Dvd.dvd :=
  ‚ü®fun _ _ _ => dvd_trans‚ü©

@[simp]
theorem dvd_mul_right (a b : Œ±) : a ‚à£ a * b :=
  Dvd.intro b rfl
#align dvd_mul_right dvd_mul_right

theorem dvd_mul_of_dvd_left (h : a ‚à£ b) (c : Œ±) : a ‚à£ b * c :=
  h.trans (dvd_mul_right b c)
#align dvd_mul_of_dvd_left dvd_mul_of_dvd_left

alias Dvd.dvd.mul_right := dvd_mul_of_dvd_left

theorem dvd_of_mul_right_dvd (h : a * b ‚à£ c) : a ‚à£ c :=
  (dvd_mul_right a b).trans h
#align dvd_of_mul_right_dvd dvd_of_mul_right_dvd

section map_dvd

variable {M N : Type*} [Monoid M] [Monoid N]

theorem map_dvd {F : Type*} [MulHomClass F M N] (f : F) {a b} : a ‚à£ b ‚Üí f a ‚à£ f b
  | ‚ü®c, h‚ü© => ‚ü®f c, h.symm ‚ñ∏ map_mul f a c‚ü©
#align map_dvd map_dvd

theorem MulHom.map_dvd (f : M ‚Üí‚Çô* N) {a b} : a ‚à£ b ‚Üí f a ‚à£ f b :=
  _root_.map_dvd f
#align mul_hom.map_dvd MulHom.map_dvd

theorem MonoidHom.map_dvd (f : M ‚Üí* N) {a b} : a ‚à£ b ‚Üí f a ‚à£ f b :=
  _root_.map_dvd f
#align monoid_hom.map_dvd MonoidHom.map_dvd

end map_dvd

end Semigroup

section Monoid

variable [Monoid Œ±] {a b : Œ±}

@[refl, simp]
theorem dvd_refl (a : Œ±) : a ‚à£ a :=
  Dvd.intro 1 (mul_one a)
#align dvd_refl dvd_refl

theorem dvd_rfl : ‚àÄ {a : Œ±}, a ‚à£ a := fun {a} => dvd_refl a
#align dvd_rfl dvd_rfl

instance : IsRefl Œ± (¬∑ ‚à£ ¬∑) :=
  ‚ü®dvd_refl‚ü©

theorem one_dvd (a : Œ±) : 1 ‚à£ a :=
  Dvd.intro a (one_mul a)
#align one_dvd one_dvd

theorem dvd_of_eq (h : a = b) : a ‚à£ b := by rw [h]
                                            -- üéâ no goals
#align dvd_of_eq dvd_of_eq

alias Eq.dvd := dvd_of_eq
#align eq.dvd Eq.dvd

end Monoid

section CommSemigroup

variable [CommSemigroup Œ±] {a b c : Œ±}

theorem Dvd.intro_left (c : Œ±) (h : c * a = b) : a ‚à£ b :=
  Dvd.intro _ (by rw [mul_comm] at h; apply h)
                  -- ‚ä¢ a * ?m.8269 c h‚úù = b
                                      -- üéâ no goals
#align dvd.intro_left Dvd.intro_left

alias dvd_of_mul_left_eq := Dvd.intro_left
#align dvd_of_mul_left_eq dvd_of_mul_left_eq

theorem exists_eq_mul_left_of_dvd (h : a ‚à£ b) : ‚àÉ c, b = c * a :=
  Dvd.elim h fun c => fun H1 : b = a * c => Exists.intro c (Eq.trans H1 (mul_comm a c))
#align exists_eq_mul_left_of_dvd exists_eq_mul_left_of_dvd

theorem dvd_iff_exists_eq_mul_left : a ‚à£ b ‚Üî ‚àÉ c, b = c * a :=
  ‚ü®exists_eq_mul_left_of_dvd, by
    rintro ‚ü®c, rfl‚ü©
    -- ‚ä¢ a ‚à£ c * a
    exact ‚ü®c, mul_comm _ _‚ü©‚ü©
    -- üéâ no goals
#align dvd_iff_exists_eq_mul_left dvd_iff_exists_eq_mul_left

theorem Dvd.elim_left {P : Prop} (h‚ÇÅ : a ‚à£ b) (h‚ÇÇ : ‚àÄ c, b = c * a ‚Üí P) : P :=
  Exists.elim (exists_eq_mul_left_of_dvd h‚ÇÅ) fun c => fun h‚ÇÉ : b = c * a => h‚ÇÇ c h‚ÇÉ
#align dvd.elim_left Dvd.elim_left

@[simp]
theorem dvd_mul_left (a b : Œ±) : a ‚à£ b * a :=
  Dvd.intro b (mul_comm a b)
#align dvd_mul_left dvd_mul_left

theorem dvd_mul_of_dvd_right (h : a ‚à£ b) (c : Œ±) : a ‚à£ c * b := by
  rw [mul_comm]; exact h.mul_right _
  -- ‚ä¢ a ‚à£ b * c
                 -- üéâ no goals
#align dvd_mul_of_dvd_right dvd_mul_of_dvd_right

alias Dvd.dvd.mul_left := dvd_mul_of_dvd_right

attribute [local simp] mul_assoc mul_comm mul_left_comm

theorem mul_dvd_mul : ‚àÄ {a b c d : Œ±}, a ‚à£ b ‚Üí c ‚à£ d ‚Üí a * c ‚à£ b * d
  | a, _, c, _, ‚ü®e, rfl‚ü©, ‚ü®f, rfl‚ü© => ‚ü®e * f, by simp‚ü©
                                                 -- üéâ no goals
#align mul_dvd_mul mul_dvd_mul

theorem dvd_of_mul_left_dvd (h : a * b ‚à£ c) : b ‚à£ c :=
  Dvd.elim h fun d ceq => Dvd.intro (a * d) (by simp [ceq])
                                                -- üéâ no goals
#align dvd_of_mul_left_dvd dvd_of_mul_left_dvd

end CommSemigroup

section CommMonoid

variable [CommMonoid Œ±] {a b : Œ±}

theorem mul_dvd_mul_left (a : Œ±) {b c : Œ±} (h : b ‚à£ c) : a * b ‚à£ a * c :=
  mul_dvd_mul (dvd_refl a) h
#align mul_dvd_mul_left mul_dvd_mul_left

theorem mul_dvd_mul_right (h : a ‚à£ b) (c : Œ±) : a * c ‚à£ b * c :=
  mul_dvd_mul h (dvd_refl c)
#align mul_dvd_mul_right mul_dvd_mul_right

theorem pow_dvd_pow_of_dvd {a b : Œ±} (h : a ‚à£ b) : ‚àÄ n : ‚Ñï, a ^ n ‚à£ b ^ n
  | 0 => by rw [pow_zero, pow_zero]
            -- üéâ no goals
  | n + 1 => by
    rw [pow_succ, pow_succ]
    -- ‚ä¢ a * a ^ n ‚à£ b * b ^ n
    exact mul_dvd_mul h (pow_dvd_pow_of_dvd h n)
    -- üéâ no goals
#align pow_dvd_pow_of_dvd pow_dvd_pow_of_dvd

end CommMonoid
