/-
Copyright (c) 2023 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Algebra.Algebra.NonUnitalSubalgebra
import Mathlib.Algebra.Star.StarAlgHom
import Mathlib.Algebra.Star.Center

/-!
# Non-unital Star Subalgebras

In this file we define `NonUnitalStarSubalgebra`s and the usual operations on them
(`map`, `comap`).

## TODO

* once we have scalar actions by semigroups (as opposed to monoids), implement the action of a
  non-unital subalgebra on the larger algebra.
-/

namespace StarMemClass

/-- If a type carries an involutive star, then any star-closed subset does too. -/
instance instInvolutiveStar {S R : Type*} [InvolutiveStar R] [SetLike S R] [StarMemClass S R]
    (s : S) : InvolutiveStar s where
  star_involutive r := Subtype.ext <| star_star (r : R)

/-- In a star semigroup (i.e., a semigroup with an antimultiplicative involutive star operation),
any star-closed subset which is also closed under multiplication is itself a star semigroup. -/
instance instStarSemigroup {S R : Type*} [Semigroup R] [StarSemigroup R] [SetLike S R]
    [MulMemClass S R] [StarMemClass S R] (s : S) : StarSemigroup s where
  star_mul _ _ := Subtype.ext <| star_mul _ _

/-- In a `StarAddMonoid` (i.e., an additive monoid with an additive involutive star operation), any
star-closed subset which is also closed under addition and contains zero is itself a
`StarAddMonoid`. -/
instance instStarAddMonoid {S R : Type*} [AddMonoid R] [StarAddMonoid R] [SetLike S R]
    [AddSubmonoidClass S R] [StarMemClass S R] (s : S) : StarAddMonoid s where
  star_add _ _ := Subtype.ext <| star_add _ _

/-- In a star ring (i.e., a non-unital semiring with an additive, antimultiplicative, involutive
star operation), an star-closed non-unital subsemiring is itself a star ring. -/
instance instStarRing {S R : Type*} [NonUnitalSemiring R] [StarRing R] [SetLike S R]
    [NonUnitalSubsemiringClass S R] [StarMemClass S R] (s : S) : StarRing s :=
  { StarMemClass.instStarSemigroup s, StarMemClass.instStarAddMonoid s with }

/-- In a star `R`-module (i.e., `star (r ‚Ä¢ m) = (star r) ‚Ä¢ m`) any star-closed subset which is also
closed under the scalar action by `R` is itself a star `R`-module. -/
instance instStarModule {S : Type*} (R : Type*) {M : Type*} [Star R] [Star M] [SMul R M]
    [StarModule R M] [SetLike S M] [SMulMemClass S R M] [StarMemClass S M] (s : S) :
    StarModule R s where
  star_smul _ _ := Subtype.ext <| star_smul _ _

end StarMemClass

universe u u' v v' w w' w''

open scoped BigOperators

variable {F : Type v'} {R' : Type u'} {R : Type u}
variable {A : Type v} {B : Type w} {C : Type w'}

namespace NonUnitalStarSubalgebraClass

variable [CommSemiring R] [NonUnitalNonAssocSemiring A]
variable [Star A] [Module R A]
variable {S : Type w''} [SetLike S A] [NonUnitalSubsemiringClass S A]
variable [hSR : SMulMemClass S R A] [StarMemClass S A] (s : S)

/-- Embedding of a non-unital star subalgebra into the non-unital star algebra. -/
def subtype (s : S) : s ‚Üí‚ãÜ‚Çô‚Çê[R] A :=
  { NonUnitalSubalgebraClass.subtype s with
    toFun := Subtype.val
    map_star' := fun _ => rfl }

@[simp]
theorem coeSubtype : (subtype s : s ‚Üí A) = Subtype.val :=
  rfl

end NonUnitalStarSubalgebraClass

/-- A non-unital star subalgebra is a non-unital subalgebra which is closed under the `star`
operation. -/
structure NonUnitalStarSubalgebra (R : Type u) (A : Type v) [CommSemiring R]
    [NonUnitalNonAssocSemiring A] [Module R A] [Star A]
    extends NonUnitalSubalgebra R A : Type v where
  /-- The `carrier` of a `NonUnitalStarSubalgebra` is closed under the `star` operation. -/
  star_mem' : ‚àÄ {a : A} (_ha : a ‚àà carrier), star a ‚àà carrier

/-- Reinterpret a `NonUnitalStarSubalgebra` as a `NonUnitalSubalgebra`. -/
add_decl_doc NonUnitalStarSubalgebra.toNonUnitalSubalgebra

namespace NonUnitalStarSubalgebra

variable [CommSemiring R]
variable [NonUnitalNonAssocSemiring A] [Module R A] [Star A]
variable [NonUnitalNonAssocSemiring B] [Module R B] [Star B]
variable [NonUnitalNonAssocSemiring C] [Module R C] [Star C]
variable [NonUnitalStarAlgHomClass F R A B]

instance instSetLike : SetLike (NonUnitalStarSubalgebra R A) A where
  coe {s} := s.carrier
  coe_injective' p q h := by cases p; cases q; congr; exact SetLike.coe_injective h
                             -- ‚ä¢ { toNonUnitalSubalgebra := toNonUnitalSubalgebra‚úù, star_mem' := star_mem'‚úù } ‚Ä¶
                                      -- ‚ä¢ { toNonUnitalSubalgebra := toNonUnitalSubalgebra‚úù¬π, star_mem' := star_mem'‚úù¬π ‚Ä¶
                                               -- ‚ä¢ toNonUnitalSubalgebra‚úù¬π = toNonUnitalSubalgebra‚úù
                                                      -- üéâ no goals

instance instNonUnitalSubsemiringClass : NonUnitalSubsemiringClass (NonUnitalStarSubalgebra R A) A
    where
  add_mem {s} := s.add_mem'
  mul_mem {s} := s.mul_mem'
  zero_mem {s} := s.zero_mem'

instance instSMulMemClass : SMulMemClass (NonUnitalStarSubalgebra R A) R A where
  smul_mem {s} := s.smul_mem'

instance instStarMemClass : StarMemClass (NonUnitalStarSubalgebra R A) A where
  star_mem {s} := s.star_mem'

instance instNonUnitalSubringClass {R : Type u} {A : Type v} [CommRing R] [NonUnitalNonAssocRing A]
    [Module R A] [Star A] : NonUnitalSubringClass (NonUnitalStarSubalgebra R A) A :=
  { NonUnitalStarSubalgebra.instNonUnitalSubsemiringClass with
    neg_mem := fun _S {x} hx => neg_one_smul R x ‚ñ∏ SMulMemClass.smul_mem _ hx }

theorem mem_carrier {s : NonUnitalStarSubalgebra R A} {x : A} : x ‚àà s.carrier ‚Üî x ‚àà s :=
  Iff.rfl

@[ext]
theorem ext {S T : NonUnitalStarSubalgebra R A} (h : ‚àÄ x : A, x ‚àà S ‚Üî x ‚àà T) : S = T :=
  SetLike.ext h

@[simp]
theorem mem_toNonUnitalSubalgebra {S : NonUnitalStarSubalgebra R A} {x} :
    x ‚àà S.toNonUnitalSubalgebra ‚Üî x ‚àà S :=
  Iff.rfl

@[simp]
theorem coe_toNonUnitalSubalgebra (S : NonUnitalStarSubalgebra R A) :
    (‚ÜëS.toNonUnitalSubalgebra : Set A) = S :=
  rfl

theorem toNonUnitalSubalgebra_injective :
    Function.Injective
      (toNonUnitalSubalgebra : NonUnitalStarSubalgebra R A ‚Üí NonUnitalSubalgebra R A) :=
  fun S T h =>
  ext fun x => by rw [‚Üê mem_toNonUnitalSubalgebra, ‚Üê mem_toNonUnitalSubalgebra, h]
                  -- üéâ no goals

theorem toNonUnitalSubalgebra_inj {S U : NonUnitalStarSubalgebra R A} :
    S.toNonUnitalSubalgebra = U.toNonUnitalSubalgebra ‚Üî S = U :=
  toNonUnitalSubalgebra_injective.eq_iff

theorem toNonUnitalSubalgebra_le_iff {S‚ÇÅ S‚ÇÇ : NonUnitalStarSubalgebra R A} :
    S‚ÇÅ.toNonUnitalSubalgebra ‚â§ S‚ÇÇ.toNonUnitalSubalgebra ‚Üî S‚ÇÅ ‚â§ S‚ÇÇ :=
  Iff.rfl

/-- Copy of a non-unital star subalgebra with a new `carrier` equal to the old one.
Useful to fix definitional equalities. -/
protected def copy (S : NonUnitalStarSubalgebra R A) (s : Set A) (hs : s = ‚ÜëS) :
    NonUnitalStarSubalgebra R A :=
  { S.toNonUnitalSubalgebra.copy s hs with
    star_mem' := @fun x (hx : x ‚àà s) => by
      show star x ‚àà s
      -- ‚ä¢ star x ‚àà s
      rw [hs] at hx ‚ä¢
      -- ‚ä¢ star x ‚àà ‚ÜëS
      exact S.star_mem' hx }
      -- üéâ no goals

@[simp]
theorem coe_copy (S : NonUnitalStarSubalgebra R A) (s : Set A) (hs : s = ‚ÜëS) :
    (S.copy s hs : Set A) = s :=
  rfl

theorem copy_eq (S : NonUnitalStarSubalgebra R A) (s : Set A) (hs : s = ‚ÜëS) : S.copy s hs = S :=
  SetLike.coe_injective hs

variable (S : NonUnitalStarSubalgebra R A)

/-- A non-unital star subalgebra over a ring is also a `Subring`. -/
def toNonUnitalSubring {R : Type u} {A : Type v} [CommRing R] [NonUnitalRing A] [Module R A]
    [Star A] (S : NonUnitalStarSubalgebra R A) : NonUnitalSubring A where
  toNonUnitalSubsemiring := S.toNonUnitalSubsemiring
  neg_mem' := neg_mem (s := S)

@[simp]
theorem mem_toNonUnitalSubring {R : Type u} {A : Type v} [CommRing R] [NonUnitalRing A] [Module R A]
    [Star A] {S : NonUnitalStarSubalgebra R A} {x} : x ‚àà S.toNonUnitalSubring ‚Üî x ‚àà S :=
  Iff.rfl

@[simp]
theorem coe_toNonUnitalSubring {R : Type u} {A : Type v} [CommRing R] [NonUnitalRing A] [Module R A]
    [Star A] (S : NonUnitalStarSubalgebra R A) : (‚ÜëS.toNonUnitalSubring : Set A) = S :=
  rfl

theorem toNonUnitalSubring_injective {R : Type u} {A : Type v} [CommRing R] [NonUnitalRing A]
    [Module R A] [Star A] :
    Function.Injective (toNonUnitalSubring : NonUnitalStarSubalgebra R A ‚Üí NonUnitalSubring A) :=
  fun S T h => ext fun x => by rw [‚Üê mem_toNonUnitalSubring, ‚Üê mem_toNonUnitalSubring, h]
                               -- üéâ no goals

theorem toNonUnitalSubring_inj {R : Type u} {A : Type v} [CommRing R] [NonUnitalRing A] [Module R A]
    [Star A] {S U : NonUnitalStarSubalgebra R A} :
    S.toNonUnitalSubring = U.toNonUnitalSubring ‚Üî S = U :=
  toNonUnitalSubring_injective.eq_iff

instance instInhabited : Inhabited S :=
  ‚ü®(0 : S.toNonUnitalSubalgebra)‚ü©

section

/-! `NonUnitalStarSubalgebra`s inherit structure from their `NonUnitalSubsemiringClass` and
`NonUnitalSubringClass` instances. -/

instance toNonUnitalSemiring {R A} [CommSemiring R] [NonUnitalSemiring A] [Module R A] [Star A]
    (S : NonUnitalStarSubalgebra R A) : NonUnitalSemiring S :=
  inferInstance

instance toNonUnitalCommSemiring {R A} [CommSemiring R] [NonUnitalCommSemiring A] [Module R A]
    [Star A] (S : NonUnitalStarSubalgebra R A) : NonUnitalCommSemiring S :=
  inferInstance

instance toNonUnitalRing {R A} [CommRing R] [NonUnitalRing A] [Module R A] [Star A]
    (S : NonUnitalStarSubalgebra R A) : NonUnitalRing S :=
  inferInstance

instance toNonUnitalCommRing {R A} [CommRing R] [NonUnitalCommRing A] [Module R A] [Star A]
    (S : NonUnitalStarSubalgebra R A) : NonUnitalCommRing S :=
  inferInstance
end

/-- The forgetful map from `NonUnitalStarSubalgebra` to `NonUnitalSubalgebra` as an
`OrderEmbedding` -/
def toNonUnitalSubalgebra' : NonUnitalStarSubalgebra R A ‚Ü™o NonUnitalSubalgebra R A where
  toEmbedding :=
    { toFun := fun S => S.toNonUnitalSubalgebra
      inj' := fun S T h => ext <| by apply SetLike.ext_iff.1 h }
                                     -- üéâ no goals
  map_rel_iff' := SetLike.coe_subset_coe.symm.trans SetLike.coe_subset_coe

section

/-! `NonUnitalStarSubalgebra`s inherit structure from their `Submodule` coercions. -/

instance module' [Semiring R'] [SMul R' R] [Module R' A] [IsScalarTower R' R A] : Module R' S :=
  SMulMemClass.toModule' _ R' R A S

instance instModule : Module R S :=
  S.module'

instance instIsScalarTower' [Semiring R'] [SMul R' R] [Module R' A] [IsScalarTower R' R A] :
    IsScalarTower R' R S :=
  S.toNonUnitalSubalgebra.instIsScalarTower'

instance instIsScalarTower [IsScalarTower R A A] : IsScalarTower R S S where
  smul_assoc r x y := Subtype.ext <| smul_assoc r (x : A) (y : A)

instance instSMulCommClass' [Semiring R'] [SMul R' R] [Module R' A] [IsScalarTower R' R A]
    [SMulCommClass R' R A] : SMulCommClass R' R S where
  smul_comm r' r s := Subtype.ext <| smul_comm r' r (s : A)

instance instSMulCommClass [SMulCommClass R A A] : SMulCommClass R S S where
  smul_comm r x y := Subtype.ext <| smul_comm r (x : A) (y : A)

end

instance noZeroSMulDivisors_bot [NoZeroSMulDivisors R A] : NoZeroSMulDivisors R S :=
  ‚ü®fun {c x} h =>
    have : c = 0 ‚à® (x : A) = 0 := eq_zero_or_eq_zero_of_smul_eq_zero (congr_arg ((‚Üë) : S ‚Üí A) h)
    this.imp_right (@Subtype.ext_iff _ _ x 0).mpr‚ü©

protected theorem coe_add (x y : S) : (‚Üë(x + y) : A) = ‚Üëx + ‚Üëy :=
  rfl

protected theorem coe_mul (x y : S) : (‚Üë(x * y) : A) = ‚Üëx * ‚Üëy :=
  rfl

protected theorem coe_zero : ((0 : S) : A) = 0 :=
  rfl

protected theorem coe_neg {R : Type u} {A : Type v} [CommRing R] [NonUnitalRing A] [Module R A]
    [Star A] {S : NonUnitalStarSubalgebra R A} (x : S) : (‚Üë(-x) : A) = -‚Üëx :=
  rfl

protected theorem coe_sub {R : Type u} {A : Type v} [CommRing R] [NonUnitalRing A] [Module R A]
    [Star A] {S : NonUnitalStarSubalgebra R A} (x y : S) : (‚Üë(x - y) : A) = ‚Üëx - ‚Üëy :=
  rfl

@[simp, norm_cast]
theorem coe_smul [Semiring R'] [SMul R' R] [Module R' A] [IsScalarTower R' R A] (r : R') (x : S) :
    (r ‚Ä¢ x : A) = r ‚Ä¢ (x : A) :=
  rfl

protected theorem coe_eq_zero {x : S} : (x : A) = 0 ‚Üî x = 0 :=
  ZeroMemClass.coe_eq_zero

@[simp]
theorem toNonUnitalSubalgebra_subtype :
    NonUnitalSubalgebraClass.subtype S = NonUnitalStarSubalgebraClass.subtype S :=
  rfl

@[simp]
theorem toSubring_subtype {R A : Type*} [CommRing R] [NonUnitalRing A] [Module R A] [Star A]
    (S : NonUnitalStarSubalgebra R A) :
    NonUnitalSubringClass.subtype S = NonUnitalStarSubalgebraClass.subtype S :=
  rfl

/-- Transport a non-unital star subalgebra via a non-unital star algebra homomorphism. -/
def map (f : F) (S : NonUnitalStarSubalgebra R A) : NonUnitalStarSubalgebra R B where
  toNonUnitalSubalgebra := S.toNonUnitalSubalgebra.map (f : A ‚Üí‚Çô‚Çê[R] B)
  star_mem' := by rintro _ ‚ü®a, ha, rfl‚ü©; exact ‚ü®star a, star_mem (s := S) ha, map_star f a‚ü©
                  -- ‚ä¢ star (‚Üë‚Üë‚Üëf a) ‚àà (NonUnitalSubalgebra.map (‚Üëf) S.toNonUnitalSubalgebra).toNon ‚Ä¶
                                         -- üéâ no goals

theorem map_mono {S‚ÇÅ S‚ÇÇ : NonUnitalStarSubalgebra R A} {f : F} :
    S‚ÇÅ ‚â§ S‚ÇÇ ‚Üí (map f S‚ÇÅ : NonUnitalStarSubalgebra R B) ‚â§ map f S‚ÇÇ :=
  Set.image_subset f

theorem map_injective {f : F} (hf : Function.Injective f) :
    Function.Injective (map f : NonUnitalStarSubalgebra R A ‚Üí NonUnitalStarSubalgebra R B) :=
  fun _S‚ÇÅ _S‚ÇÇ ih =>
  ext <| Set.ext_iff.1 <| Set.image_injective.2 hf <| Set.ext <| SetLike.ext_iff.mp ih

@[simp]
theorem map_id (S : NonUnitalStarSubalgebra R A) : map (NonUnitalStarAlgHom.id R A) S = S :=
  SetLike.coe_injective <| Set.image_id _

theorem map_map (S : NonUnitalStarSubalgebra R A) (g : B ‚Üí‚ãÜ‚Çô‚Çê[R] C) (f : A ‚Üí‚ãÜ‚Çô‚Çê[R] B) :
    (S.map f).map g = S.map (g.comp f) :=
  SetLike.coe_injective <| Set.image_image _ _ _

@[simp]
theorem mem_map {S : NonUnitalStarSubalgebra R A} {f : F} {y : B} :
    y ‚àà map f S ‚Üî ‚àÉ x ‚àà S, f x = y :=
  NonUnitalSubalgebra.mem_map

theorem map_toNonUnitalSubalgebra {S : NonUnitalStarSubalgebra R A} {f : F} :
    (map f S : NonUnitalStarSubalgebra R B).toNonUnitalSubalgebra =
      NonUnitalSubalgebra.map f S.toNonUnitalSubalgebra :=
  SetLike.coe_injective rfl

@[simp]
theorem coe_map (S : NonUnitalStarSubalgebra R A) (f : F) : map f S = f '' S :=
  rfl

/-- Preimage of a non-unital star subalgebra under a non-unital star algebra homomorphism. -/
def comap (f : F) (S : NonUnitalStarSubalgebra R B) : NonUnitalStarSubalgebra R A where
  toNonUnitalSubalgebra := S.toNonUnitalSubalgebra.comap f
  star_mem' := @fun a (ha : f a ‚àà S) =>
    show f (star a) ‚àà S from (map_star f a).symm ‚ñ∏ star_mem (s := S) ha

theorem map_le {S : NonUnitalStarSubalgebra R A} {f : F} {U : NonUnitalStarSubalgebra R B} :
    map f S ‚â§ U ‚Üî S ‚â§ comap f U :=
  Set.image_subset_iff

theorem gc_map_comap (f : F) : GaloisConnection (map f) (comap f) :=
  fun _S _U => map_le

@[simp]
theorem mem_comap (S : NonUnitalStarSubalgebra R B) (f : F) (x : A) : x ‚àà comap f S ‚Üî f x ‚àà S :=
  Iff.rfl

@[simp, norm_cast]
theorem coe_comap (S : NonUnitalStarSubalgebra R B) (f : F) : comap f S = f ‚Åª¬π' (S : Set B) :=
  rfl

instance instNoZeroDivisors {R A : Type*} [CommSemiring R] [NonUnitalSemiring A] [NoZeroDivisors A]
    [Module R A] [Star A] (S : NonUnitalStarSubalgebra R A) : NoZeroDivisors S :=
  NonUnitalSubsemiringClass.noZeroDivisors S

end NonUnitalStarSubalgebra

namespace NonUnitalSubalgebra

variable [CommSemiring R] [NonUnitalSemiring A] [Module R A] [Star A]
variable (s : NonUnitalSubalgebra R A)

/-- A non-unital subalgebra closed under `star` is a non-unital star subalgebra. -/
def toNonUnitalStarSubalgebra (h_star : ‚àÄ x, x ‚àà s ‚Üí star x ‚àà s) : NonUnitalStarSubalgebra R A :=
  { s with
    star_mem' := @h_star }

@[simp]
theorem mem_toNonUnitalStarSubalgebra {s : NonUnitalSubalgebra R A} {h_star} {x} :
    x ‚àà s.toNonUnitalStarSubalgebra h_star ‚Üî x ‚àà s :=
  Iff.rfl

@[simp]
theorem coe_toNonUnitalStarSubalgebra (s : NonUnitalSubalgebra R A) (h_star) :
    (s.toNonUnitalStarSubalgebra h_star : Set A) = s :=
  rfl

@[simp]
theorem toNonUnitalStarSubalgebra_toNonUnitalSubalgebra (s : NonUnitalSubalgebra R A) (h_star) :
    (s.toNonUnitalStarSubalgebra h_star).toNonUnitalSubalgebra = s :=
  SetLike.coe_injective rfl

@[simp]
theorem _root_.NonUnitalStarSubalgebra.toNonUnitalSubalgebra_toNonUnitalStarSubalgebra
    (S : NonUnitalStarSubalgebra R A) :
    (S.toNonUnitalSubalgebra.toNonUnitalStarSubalgebra fun _ => star_mem (s := S)) = S :=
  SetLike.coe_injective rfl

end NonUnitalSubalgebra
namespace NonUnitalStarAlgHom

variable [CommSemiring R]
variable [NonUnitalNonAssocSemiring A] [Module R A] [Star A]
variable [NonUnitalNonAssocSemiring B] [Module R B] [Star B]
variable [NonUnitalNonAssocSemiring C] [Module R C] [Star C]
variable [NonUnitalStarAlgHomClass F R A B]

/-- Range of an `NonUnitalAlgHom` as a `NonUnitalStarSubalgebra`. -/
protected def range (œÜ : F) : NonUnitalStarSubalgebra R B where
  toNonUnitalSubalgebra := NonUnitalAlgHom.range (œÜ : A ‚Üí‚Çô‚Çê[R] B)
  star_mem' := by rintro _ ‚ü®a, rfl‚ü©; exact ‚ü®star a, map_star œÜ a‚ü©
                  -- ‚ä¢ star (‚Üë‚Üë‚ÜëœÜ a) ‚àà (NonUnitalAlgHom.range ‚ÜëœÜ).toNonUnitalSubsemiring.toAddSubmo ‚Ä¶
                                     -- üéâ no goals

@[simp]
theorem mem_range (œÜ : F) {y : B} :
    y ‚àà (NonUnitalStarAlgHom.range œÜ : NonUnitalStarSubalgebra R B) ‚Üî ‚àÉ x : A, œÜ x = y :=
  NonUnitalRingHom.mem_srange

theorem mem_range_self (œÜ : F) (x : A) :
    œÜ x ‚àà (NonUnitalStarAlgHom.range œÜ : NonUnitalStarSubalgebra R B) :=
  (NonUnitalAlgHom.mem_range œÜ).2 ‚ü®x, rfl‚ü©

@[simp]
theorem coe_range (œÜ : F) :
    ((NonUnitalStarAlgHom.range œÜ : NonUnitalStarSubalgebra R B) : Set B) = Set.range (œÜ : A ‚Üí B) :=
  by ext; rw [SetLike.mem_coe, mem_range]; rfl
     -- ‚ä¢ x‚úù ‚àà ‚Üë(NonUnitalStarAlgHom.range œÜ) ‚Üî x‚úù ‚àà Set.range ‚ÜëœÜ
          -- ‚ä¢ (‚àÉ x, ‚ÜëœÜ x = x‚úù) ‚Üî x‚úù ‚àà Set.range ‚ÜëœÜ
                                           -- üéâ no goals

theorem range_comp (f : A ‚Üí‚ãÜ‚Çô‚Çê[R] B) (g : B ‚Üí‚ãÜ‚Çô‚Çê[R] C) :
  NonUnitalStarAlgHom.range (g.comp f) = (NonUnitalStarAlgHom.range f).map g :=
  SetLike.coe_injective (Set.range_comp g f)

theorem range_comp_le_range (f : A ‚Üí‚ãÜ‚Çô‚Çê[R] B) (g : B ‚Üí‚ãÜ‚Çô‚Çê[R] C) :
  NonUnitalStarAlgHom.range (g.comp f) ‚â§ NonUnitalStarAlgHom.range g :=
  SetLike.coe_mono (Set.range_comp_subset_range f g)

/-- Restrict the codomain of a non-unital star algebra homomorphism. -/
def codRestrict (f : F) (S : NonUnitalStarSubalgebra R B) (hf : ‚àÄ x, f x ‚àà S) : A ‚Üí‚ãÜ‚Çô‚Çê[R] S where
  toNonUnitalAlgHom := NonUnitalAlgHom.codRestrict f S.toNonUnitalSubalgebra hf
  map_star' := fun a => Subtype.ext <| map_star f a

@[simp]
theorem subtype_comp_codRestrict (f : F) (S : NonUnitalStarSubalgebra R B) (hf : ‚àÄ x : A, f x ‚àà S) :
    (NonUnitalStarSubalgebraClass.subtype S).comp (NonUnitalStarAlgHom.codRestrict f S hf) = f :=
  NonUnitalStarAlgHom.ext fun _ => rfl

@[simp]
theorem coe_codRestrict (f : F) (S : NonUnitalStarSubalgebra R B) (hf : ‚àÄ x, f x ‚àà S) (x : A) :
    ‚Üë(NonUnitalStarAlgHom.codRestrict f S hf x) = f x :=
  rfl

theorem injective_codRestrict (f : F) (S : NonUnitalStarSubalgebra R B) (hf : ‚àÄ x : A, f x ‚àà S) :
    Function.Injective (NonUnitalStarAlgHom.codRestrict f S hf) ‚Üî Function.Injective f :=
  ‚ü®fun H _x _y hxy => H <| Subtype.eq hxy, fun H _x _y hxy => H (congr_arg Subtype.val hxy : _)‚ü©

/-- Restrict the codomain of a non-unital star algebra homomorphism `f` to `f.range`.

This is the bundled version of `Set.rangeFactorization`. -/
@[reducible]
def rangeRestrict (f : F) : A ‚Üí‚ãÜ‚Çô‚Çê[R] (NonUnitalStarAlgHom.range f : NonUnitalStarSubalgebra R B) :=
  NonUnitalStarAlgHom.codRestrict f (NonUnitalStarAlgHom.range f)
    (NonUnitalStarAlgHom.mem_range_self f)

/-- The equalizer of two non-unital star `R`-algebra homomorphisms -/
def equalizer (œï œà : F) : NonUnitalStarSubalgebra R A where
  toNonUnitalSubalgebra := NonUnitalAlgHom.equalizer œï œà
  star_mem' := @fun x (hx : œï x = œà x) => by simp [map_star, hx]
                                             -- üéâ no goals

@[simp]
theorem mem_equalizer (œÜ œà : F) (x : A) :
    x ‚àà NonUnitalStarAlgHom.equalizer œÜ œà ‚Üî œÜ x = œà x :=
  Iff.rfl

end NonUnitalStarAlgHom

namespace StarAlgEquiv
variable [CommSemiring R]
variable [NonUnitalSemiring A] [Module R A] [Star A]
variable [NonUnitalSemiring B] [Module R B] [Star B]
variable [NonUnitalSemiring C] [Module R C] [Star C]
variable [hF : NonUnitalStarAlgHomClass F R A B]

/-- Restrict a non-unital star algebra homomorphism with a left inverse to an algebra isomorphism
to its range.

This is a computable alternative to `StarAlgEquiv.ofInjective`. -/
def ofLeftInverse' {g : B ‚Üí A} {f : F} (h : Function.LeftInverse g f) :
    A ‚âÉ‚ãÜ‚Çê[R] NonUnitalStarAlgHom.range f :=
  { NonUnitalStarAlgHom.rangeRestrict f with
    toFun := NonUnitalStarAlgHom.rangeRestrict f
    invFun := g ‚àò (NonUnitalStarSubalgebraClass.subtype <| NonUnitalStarAlgHom.range f)
    left_inv := h
    right_inv := fun x =>
      Subtype.ext <|
        let ‚ü®x', hx'‚ü© := (NonUnitalStarAlgHom.mem_range f).mp x.prop
        show f (g x) = x by rw [‚Üê hx', h x'] }
                            -- üéâ no goals

@[simp]
theorem ofLeftInverse'_apply {g : B ‚Üí A} {f : F} (h : Function.LeftInverse g f) (x : A) :
    ofLeftInverse' h x = f x :=
  rfl

@[simp]
theorem ofLeftInverse'_symm_apply {g : B ‚Üí A} {f : F} (h : Function.LeftInverse g f)
    (x : NonUnitalStarAlgHom.range f) : (ofLeftInverse' h).symm x = g x :=
  rfl

/-- Restrict an injective non-unital star algebra homomorphism to a star algebra isomorphism -/
noncomputable def ofInjective' (f : F) (hf : Function.Injective f) :
    A ‚âÉ‚ãÜ‚Çê[R] NonUnitalStarAlgHom.range f :=
  ofLeftInverse' (Classical.choose_spec hf.hasLeftInverse)

@[simp]
theorem ofInjective'_apply (f : F) (hf : Function.Injective f) (x : A) :
    ofInjective' f hf x = f x :=
  rfl

end StarAlgEquiv

/-! ### The star closure of a subalgebra -/


namespace NonUnitalSubalgebra

open scoped Pointwise

variable [CommSemiring R] [StarRing R]
variable [NonUnitalSemiring A] [StarRing A] [Module R A]
variable [IsScalarTower R A A] [SMulCommClass R A A] [StarModule R A]
variable [NonUnitalSemiring B] [StarRing B] [Module R B]
variable [IsScalarTower R B B] [SMulCommClass R B B] [StarModule R B]

/-- The pointwise `star` of a non-unital subalgebra is a non-unital subalgebra. -/
instance instInvolutiveStar : InvolutiveStar (NonUnitalSubalgebra R A)
    where
  star S :=
    { carrier := star S.carrier
      mul_mem' := @fun x y hx hy => by simpa only [Set.mem_star, NonUnitalSubalgebra.mem_carrier]
        using (star_mul x y).symm ‚ñ∏ mul_mem hy hx
      add_mem' := @fun x y hx hy => by simpa only [Set.mem_star, NonUnitalSubalgebra.mem_carrier]
        using (star_add x y).symm ‚ñ∏ add_mem hx hy
      zero_mem' := Set.mem_star.mp ((star_zero A).symm ‚ñ∏ zero_mem S : star (0 : A) ‚àà S)
      smul_mem' := fun r x hx => by simpa only [Set.mem_star, NonUnitalSubalgebra.mem_carrier]
        using (star_smul r x).symm ‚ñ∏ SMulMemClass.smul_mem (star r) hx }
  star_involutive S := NonUnitalSubalgebra.ext fun x =>
      ‚ü®fun hx => star_star x ‚ñ∏ hx, fun hx => ((star_star x).symm ‚ñ∏ hx : star (star x) ‚àà S)‚ü©

@[simp]
theorem mem_star_iff (S : NonUnitalSubalgebra R A) (x : A) : x ‚àà star S ‚Üî star x ‚àà S :=
  Iff.rfl

theorem star_mem_star_iff (S : NonUnitalSubalgebra R A) (x : A) : star x ‚àà star S ‚Üî x ‚àà S := by
  simp
  -- üéâ no goals

@[simp]
theorem coe_star (S : NonUnitalSubalgebra R A) : star S = star (S : Set A) :=
  rfl

theorem star_mono : Monotone (star : NonUnitalSubalgebra R A ‚Üí NonUnitalSubalgebra R A) :=
  fun _ _ h _ hx => h hx

variable (R)

/-- The star operation on `NonUnitalSubalgebra` commutes with `NonUnitalAlgebra.adjoin`. -/
theorem star_adjoin_comm (s : Set A) :
    star (NonUnitalAlgebra.adjoin R s) = NonUnitalAlgebra.adjoin R (star s) :=
  have this :
    ‚àÄ t : Set A, NonUnitalAlgebra.adjoin R (star t) ‚â§ star (NonUnitalAlgebra.adjoin R t) := fun t =>
    NonUnitalAlgebra.adjoin_le fun x hx => NonUnitalAlgebra.subset_adjoin R hx
  le_antisymm (by simpa only [star_star] using NonUnitalSubalgebra.star_mono (this (star s)))
                  -- üéâ no goals
    (this s)

variable {R}

/-- The `NonUnitalStarSubalgebra` obtained from `S : NonUnitalSubalgebra R A` by taking the
smallest non-unital subalgebra containing both `S` and `star S`. -/
@[simps!]
def starClosure (S : NonUnitalSubalgebra R A) : NonUnitalStarSubalgebra R A where
  toNonUnitalSubalgebra := S ‚äî star S
  star_mem' := @fun a (ha : a ‚àà S ‚äî star S) => show star a ‚àà S ‚äî star S by
    simp only [‚Üê mem_star_iff _ a, ‚Üê (@NonUnitalAlgebra.gi R A _ _ _ _ _).l_sup_u _ _] at *
    -- ‚ä¢ a ‚àà star (NonUnitalAlgebra.adjoin R (‚ÜëS ‚äî ‚Üë(star S)))
    convert ha using 2
    -- ‚ä¢ star (NonUnitalAlgebra.adjoin R (‚ÜëS ‚äî ‚Üë(star S))) = NonUnitalAlgebra.adjoin  ‚Ä¶
    simp only [Set.sup_eq_union, star_adjoin_comm, Set.union_star, coe_star, star_star,
      Set.union_comm]

theorem starClosure_le {S‚ÇÅ : NonUnitalSubalgebra R A} {S‚ÇÇ : NonUnitalStarSubalgebra R A}
    (h : S‚ÇÅ ‚â§ S‚ÇÇ.toNonUnitalSubalgebra) : S‚ÇÅ.starClosure ‚â§ S‚ÇÇ :=
  NonUnitalStarSubalgebra.toNonUnitalSubalgebra_le_iff.1 <|
    sup_le h fun x hx =>
      (star_star x ‚ñ∏ star_mem (show star x ‚àà S‚ÇÇ from h <| (S‚ÇÅ.mem_star_iff _).1 hx) : x ‚àà S‚ÇÇ)

theorem starClosure_le_iff {S‚ÇÅ : NonUnitalSubalgebra R A} {S‚ÇÇ : NonUnitalStarSubalgebra R A} :
    S‚ÇÅ.starClosure ‚â§ S‚ÇÇ ‚Üî S‚ÇÅ ‚â§ S‚ÇÇ.toNonUnitalSubalgebra :=
  ‚ü®fun h => le_sup_left.trans h, starClosure_le‚ü©

@[simp]
theorem starClosure_toNonunitalSubalgebra {S : NonUnitalSubalgebra R A} :
    S.starClosure.toNonUnitalSubalgebra = S ‚äî star S :=
  rfl

@[mono]
theorem starClosure_mono : Monotone (starClosure (R := R) (A := A)) :=
  fun _ _ h => starClosure_le <| h.trans le_sup_left

end NonUnitalSubalgebra

namespace NonUnitalStarAlgebra

variable [CommSemiring R] [StarRing R]
variable [NonUnitalSemiring A] [StarRing A]
variable [Module R A] [IsScalarTower R A A] [SMulCommClass R A A] [StarModule R A]
variable [NonUnitalSemiring B] [StarRing B]
variable [Module R B] [IsScalarTower R B B] [SMulCommClass R B B] [StarModule R B]
variable [hF : NonUnitalStarAlgHomClass F R A B]

open scoped Pointwise

open NonUnitalStarSubalgebra

variable (R)

/-- The minimal non-unital subalgebra that includes `s`. -/
def adjoin (s : Set A) : NonUnitalStarSubalgebra R A where
  toNonUnitalSubalgebra := NonUnitalAlgebra.adjoin R (s ‚à™ star s)
  star_mem' _ := by
    rwa [NonUnitalSubalgebra.mem_carrier, ‚Üê NonUnitalSubalgebra.mem_star_iff,
      NonUnitalSubalgebra.star_adjoin_comm, Set.union_star, star_star, Set.union_comm]

theorem adjoin_eq_starClosure_adjoin (s : Set A) :
    adjoin R s = (NonUnitalAlgebra.adjoin R s).starClosure :=
  toNonUnitalSubalgebra_injective <| show
    NonUnitalAlgebra.adjoin R (s ‚à™ star s) =
      NonUnitalAlgebra.adjoin R s ‚äî star (NonUnitalAlgebra.adjoin R s)
    from
      (NonUnitalSubalgebra.star_adjoin_comm R s).symm ‚ñ∏ NonUnitalAlgebra.adjoin_union s (star s)

theorem adjoin_toNonUnitalSubalgebra (s : Set A) :
    (adjoin R s).toNonUnitalSubalgebra = NonUnitalAlgebra.adjoin R (s ‚à™ star s) :=
  rfl

theorem subset_adjoin (s : Set A) : s ‚äÜ adjoin R s :=
  (Set.subset_union_left s (star s)).trans <| NonUnitalAlgebra.subset_adjoin R

theorem star_subset_adjoin (s : Set A) : star s ‚äÜ adjoin R s :=
  (Set.subset_union_right s (star s)).trans <| NonUnitalAlgebra.subset_adjoin R

theorem self_mem_adjoin_singleton (x : A) : x ‚àà adjoin R ({x} : Set A) :=
  NonUnitalAlgebra.subset_adjoin R <| Set.mem_union_left _ (Set.mem_singleton x)

theorem star_self_mem_adjoin_singleton (x : A) : star x ‚àà adjoin R ({x} : Set A) :=
  star_mem <| self_mem_adjoin_singleton R x

variable {R}

protected theorem gc : GaloisConnection (adjoin R : Set A ‚Üí NonUnitalStarSubalgebra R A) (‚Üë) := by
  intro s S
  -- ‚ä¢ adjoin R s ‚â§ S ‚Üî s ‚â§ ‚ÜëS
  rw [‚Üê toNonUnitalSubalgebra_le_iff, adjoin_toNonUnitalSubalgebra,
    NonUnitalAlgebra.adjoin_le_iff, coe_toNonUnitalSubalgebra]
  exact ‚ü®fun h => (Set.subset_union_left s _).trans h,
    fun h => Set.union_subset h fun x hx => star_star x ‚ñ∏ star_mem (show star x ‚àà S from h hx)‚ü©

/-- Galois insertion between `adjoin` and `Subtype.val`. -/
protected def gi : GaloisInsertion (adjoin R : Set A ‚Üí NonUnitalStarSubalgebra R A) (‚Üë) where
  choice s hs := (adjoin R s).copy s <| le_antisymm (NonUnitalStarAlgebra.gc.le_u_l s) hs
  gc := NonUnitalStarAlgebra.gc
  le_l_u S := (NonUnitalStarAlgebra.gc (S : Set A) (adjoin R S)).1 <| le_rfl
  choice_eq _ _ := NonUnitalStarSubalgebra.copy_eq _ _ _

theorem adjoin_le {S : NonUnitalStarSubalgebra R A} {s : Set A} (hs : s ‚äÜ S) : adjoin R s ‚â§ S :=
  NonUnitalStarAlgebra.gc.l_le hs

theorem adjoin_le_iff {S : NonUnitalStarSubalgebra R A} {s : Set A} : adjoin R s ‚â§ S ‚Üî s ‚äÜ S :=
  NonUnitalStarAlgebra.gc _ _

theorem _root_.NonUnitalSubalgebra.starClosure_eq_adjoin (S : NonUnitalSubalgebra R A) :
    S.starClosure = adjoin R (S : Set A) :=
  le_antisymm (NonUnitalSubalgebra.starClosure_le_iff.2 <| subset_adjoin R (S : Set A))
    (adjoin_le (le_sup_left : S ‚â§ S ‚äî star S))

instance : CompleteLattice (NonUnitalStarSubalgebra R A) :=
  GaloisInsertion.liftCompleteLattice NonUnitalStarAlgebra.gi

@[simp]
theorem coe_top : ((‚ä§ : NonUnitalStarSubalgebra R A) : Set A) = Set.univ :=
  rfl

@[simp]
theorem mem_top {x : A} : x ‚àà (‚ä§ : NonUnitalStarSubalgebra R A) :=
  Set.mem_univ x

@[simp]
theorem top_toNonUnitalSubalgebra :
    (‚ä§ : NonUnitalStarSubalgebra R A).toNonUnitalSubalgebra = ‚ä§ := by ext; simp
                                                                      -- ‚ä¢ x‚úù ‚àà ‚ä§.toNonUnitalSubalgebra ‚Üî x‚úù ‚àà ‚ä§
                                                                           -- üéâ no goals

@[simp]
theorem toNonUnitalSubalgebra_eq_top {S : NonUnitalStarSubalgebra R A} :
    S.toNonUnitalSubalgebra = ‚ä§ ‚Üî S = ‚ä§ :=
  NonUnitalStarSubalgebra.toNonUnitalSubalgebra_injective.eq_iff' top_toNonUnitalSubalgebra

theorem mem_sup_left {S T : NonUnitalStarSubalgebra R A} : ‚àÄ {x : A}, x ‚àà S ‚Üí x ‚àà S ‚äî T := by
  rw [‚ÜêSetLike.le_def]
  -- ‚ä¢ S ‚â§ S ‚äî T
  exact le_sup_left
  -- üéâ no goals

theorem mem_sup_right {S T : NonUnitalStarSubalgebra R A} : ‚àÄ {x : A}, x ‚àà T ‚Üí x ‚àà S ‚äî T := by
  rw [‚ÜêSetLike.le_def]
  -- ‚ä¢ T ‚â§ S ‚äî T
  exact le_sup_right
  -- üéâ no goals

theorem mul_mem_sup {S T : NonUnitalStarSubalgebra R A} {x y : A} (hx : x ‚àà S) (hy : y ‚àà T) :
    x * y ‚àà S ‚äî T :=
  mul_mem (mem_sup_left hx) (mem_sup_right hy)

theorem map_sup (f : F) (S T : NonUnitalStarSubalgebra R A) :
    ((S ‚äî T).map f : NonUnitalStarSubalgebra R B) = S.map f ‚äî T.map f :=
  (NonUnitalStarSubalgebra.gc_map_comap f).l_sup

@[simp, norm_cast]
theorem coe_inf (S T : NonUnitalStarSubalgebra R A) : (‚Üë(S ‚äì T) : Set A) = (S : Set A) ‚à© T :=
  rfl

@[simp]
theorem mem_inf {S T : NonUnitalStarSubalgebra R A} {x : A} : x ‚àà S ‚äì T ‚Üî x ‚àà S ‚àß x ‚àà T :=
  Iff.rfl

@[simp]
theorem inf_toNonUnitalSubalgebra (S T : NonUnitalStarSubalgebra R A) :
    (S ‚äì T).toNonUnitalSubalgebra = S.toNonUnitalSubalgebra ‚äì T.toNonUnitalSubalgebra :=
  SetLike.coe_injective <| coe_inf _ _
  -- it's a bit surprising `rfl` fails here.

@[simp, norm_cast]
theorem coe_sInf (S : Set (NonUnitalStarSubalgebra R A)) : (‚Üë(sInf S) : Set A) = ‚ãÇ s ‚àà S, ‚Üës :=
  sInf_image

theorem mem_sInf {S : Set (NonUnitalStarSubalgebra R A)} {x : A} : x ‚àà sInf S ‚Üî ‚àÄ p ‚àà S, x ‚àà p := by
  simp only [‚Üê SetLike.mem_coe, coe_sInf, Set.mem_iInter‚ÇÇ]
  -- üéâ no goals

@[simp]
theorem sInf_toNonUnitalSubalgebra (S : Set (NonUnitalStarSubalgebra R A)) :
    (sInf S).toNonUnitalSubalgebra = sInf (NonUnitalStarSubalgebra.toNonUnitalSubalgebra '' S) :=
  SetLike.coe_injective <| by simp
                              -- üéâ no goals

@[simp, norm_cast]
theorem coe_iInf {Œπ : Sort*} {S : Œπ ‚Üí NonUnitalStarSubalgebra R A} :
    (‚Üë(‚®Ö i, S i) : Set A) = ‚ãÇ i, S i := by simp [iInf]
                                           -- üéâ no goals

theorem mem_iInf {Œπ : Sort*} {S : Œπ ‚Üí NonUnitalStarSubalgebra R A} {x : A} :
    (x ‚àà ‚®Ö i, S i) ‚Üî ‚àÄ i, x ‚àà S i := by simp only [iInf, mem_sInf, Set.forall_range_iff]
                                        -- üéâ no goals

@[simp]
theorem iInf_toNonUnitalSubalgebra {Œπ : Sort*} (S : Œπ ‚Üí NonUnitalStarSubalgebra R A) :
    (‚®Ö i, S i).toNonUnitalSubalgebra = ‚®Ö i, (S i).toNonUnitalSubalgebra :=
  SetLike.coe_injective <| by simp
                              -- üéâ no goals

instance : Inhabited (NonUnitalStarSubalgebra R A) :=
  ‚ü®‚ä•‚ü©

theorem mem_bot {x : A} : x ‚àà (‚ä• : NonUnitalStarSubalgebra R A) ‚Üî x = 0 :=
  show x ‚àà NonUnitalAlgebra.adjoin R (‚àÖ ‚à™ star ‚àÖ : Set A) ‚Üî x = 0 by
    rw [Set.star_empty, Set.union_empty, NonUnitalAlgebra.adjoin_empty, NonUnitalAlgebra.mem_bot]
    -- üéâ no goals

theorem toNonUnitalSubalgebra_bot :
    (‚ä• : NonUnitalStarSubalgebra R A).toNonUnitalSubalgebra = ‚ä• := by
  ext x
  -- ‚ä¢ x ‚àà ‚ä•.toNonUnitalSubalgebra ‚Üî x ‚àà ‚ä•
  simp only [mem_bot, NonUnitalStarSubalgebra.mem_toNonUnitalSubalgebra, NonUnitalAlgebra.mem_bot]
  -- üéâ no goals

@[simp]
theorem coe_bot : ((‚ä• : NonUnitalStarSubalgebra R A) : Set A) = {0} := by
  simp only [Set.ext_iff, NonUnitalStarAlgebra.mem_bot, SetLike.mem_coe, Set.mem_singleton_iff,
    iff_self_iff, forall_const]

theorem eq_top_iff {S : NonUnitalStarSubalgebra R A} : S = ‚ä§ ‚Üî ‚àÄ x : A, x ‚àà S :=
  ‚ü®fun h x => by rw [h]; exact mem_top,
                 -- ‚ä¢ x ‚àà ‚ä§
                         -- üéâ no goals
    fun h => by ext x; exact ‚ü®fun _ => mem_top, fun _ => h x‚ü©‚ü©
                -- ‚ä¢ x ‚àà S ‚Üî x ‚àà ‚ä§
                       -- üéâ no goals

theorem range_top_iff_surjective (f : F) :
    NonUnitalStarAlgHom.range f = (‚ä§ : NonUnitalStarSubalgebra R B) ‚Üî Function.Surjective f :=
  NonUnitalStarAlgebra.eq_top_iff

@[simp]
theorem range_id : NonUnitalStarAlgHom.range (NonUnitalStarAlgHom.id R A) = ‚ä§ :=
  SetLike.coe_injective Set.range_id

@[simp]
theorem map_top (f : F) : (‚ä§ : NonUnitalStarSubalgebra R A).map f = NonUnitalStarAlgHom.range f :=
  SetLike.coe_injective Set.image_univ

@[simp]
theorem map_bot (f : F) : (‚ä• : NonUnitalStarSubalgebra R A).map f = ‚ä• :=
  SetLike.coe_injective <| by simp [NonUnitalAlgebra.coe_bot, NonUnitalStarSubalgebra.coe_map]
                              -- üéâ no goals

@[simp]
theorem comap_top (f : F) : (‚ä§ : NonUnitalStarSubalgebra R B).comap f = ‚ä§ :=
  eq_top_iff.2 fun _x => mem_top

/-- `NonUnitalStarAlgHom` to `‚ä§ : NonUnitalStarSubalgebra R A`. -/
def toTop : A ‚Üí‚ãÜ‚Çô‚Çê[R] (‚ä§ : NonUnitalStarSubalgebra R A) :=
  NonUnitalStarAlgHom.codRestrict (NonUnitalStarAlgHom.id R A) ‚ä§ fun _ => mem_top

end NonUnitalStarAlgebra

namespace NonUnitalStarSubalgebra

open NonUnitalStarAlgebra

variable [CommSemiring R] [StarRing R]
variable [NonUnitalSemiring A] [StarRing A]
variable [Module R A] [IsScalarTower R A A] [SMulCommClass R A A] [StarModule R A]
variable [NonUnitalSemiring B] [StarRing B]
variable [Module R B] [IsScalarTower R B B] [SMulCommClass R B B] [StarModule R B]
variable [hF : NonUnitalStarAlgHomClass F R A B] (S : NonUnitalStarSubalgebra R A)

instance subsingleton_of_subsingleton [Subsingleton A] :
    Subsingleton (NonUnitalStarSubalgebra R A) :=
  ‚ü®fun B C => ext fun x => by simp only [Subsingleton.elim x 0, zero_mem B, zero_mem C]‚ü©
                              -- üéâ no goals

instance _root_.NonUnitalStarAlgHom.subsingleton [Subsingleton (NonUnitalStarSubalgebra R A)] :
    Subsingleton (A ‚Üí‚ãÜ‚Çô‚Çê[R] B) :=
  ‚ü®fun f g => NonUnitalStarAlgHom.ext fun a =>
    have : a ‚àà (‚ä• : NonUnitalStarSubalgebra R A) :=
      Subsingleton.elim (‚ä§ : NonUnitalStarSubalgebra R A) ‚ä• ‚ñ∏ mem_top
    (mem_bot.mp this).symm ‚ñ∏ (map_zero f).trans (map_zero g).symm‚ü©

theorem range_val : NonUnitalStarAlgHom.range (NonUnitalStarSubalgebraClass.subtype S) = S :=
  ext <| Set.ext_iff.1 <| (NonUnitalStarSubalgebraClass.subtype S).coe_range.trans Subtype.range_val

/--
The map `S ‚Üí T` when `S` is a non-unital star subalgebra contained in the non-unital star
algebra `T`.

This is the non-unital star subalgebra version of `Submodule.ofLe`, or
`NonUnitalSubalgebra.inclusion`  -/
def inclusion {S T : NonUnitalStarSubalgebra R A} (h : S ‚â§ T) : S ‚Üí‚ãÜ‚Çô‚Çê[R] T where
  toNonUnitalAlgHom := NonUnitalSubalgebra.inclusion h
  map_star' _ := rfl

theorem inclusion_injective {S T : NonUnitalStarSubalgebra R A} (h : S ‚â§ T) :
    Function.Injective (inclusion h) :=
  fun _ _ => Subtype.ext ‚àò Subtype.mk.inj

@[simp]
theorem inclusion_self {S : NonUnitalStarSubalgebra R A} :
    inclusion (le_refl S) = NonUnitalAlgHom.id R S :=
  NonUnitalAlgHom.ext fun _x => Subtype.ext rfl

@[simp]
theorem inclusion_mk {S T : NonUnitalStarSubalgebra R A} (h : S ‚â§ T) (x : A) (hx : x ‚àà S) :
    inclusion h ‚ü®x, hx‚ü© = ‚ü®x, h hx‚ü© :=
  rfl

theorem inclusion_right {S T : NonUnitalStarSubalgebra R A} (h : S ‚â§ T) (x : T) (m : (x : A) ‚àà S) :
    inclusion h ‚ü®x, m‚ü© = x :=
  Subtype.ext rfl

@[simp]
theorem inclusion_inclusion {S T U : NonUnitalStarSubalgebra R A} (hst : S ‚â§ T) (htu : T ‚â§ U)
    (x : S) : inclusion htu (inclusion hst x) = inclusion (le_trans hst htu) x :=
  Subtype.ext rfl

@[simp]
theorem val_inclusion {S T : NonUnitalStarSubalgebra R A} (h : S ‚â§ T) (s : S) :
    (inclusion h s : A) = s :=
  rfl
section Prod

variable (S‚ÇÅ : NonUnitalStarSubalgebra R B)

/-- The product of two non-unital star subalgebras is a non-unital star subalgebra. -/
def prod : NonUnitalStarSubalgebra R (A √ó B) :=
  { S.toNonUnitalSubalgebra.prod S‚ÇÅ.toNonUnitalSubalgebra with
    carrier := S √óÀ¢ S‚ÇÅ
    star_mem' := fun hx => ‚ü®star_mem hx.1, star_mem hx.2‚ü© }

@[simp]
theorem coe_prod : (prod S S‚ÇÅ : Set (A √ó B)) = (S : Set A) √óÀ¢ S‚ÇÅ :=
  rfl

theorem prod_toNonUnitalSubalgebra :
    (S.prod S‚ÇÅ).toNonUnitalSubalgebra = S.toNonUnitalSubalgebra.prod S‚ÇÅ.toNonUnitalSubalgebra :=
  rfl

@[simp]
theorem mem_prod {S : NonUnitalStarSubalgebra R A} {S‚ÇÅ : NonUnitalStarSubalgebra R B} {x : A √ó B} :
    x ‚àà prod S S‚ÇÅ ‚Üî x.1 ‚àà S ‚àß x.2 ‚àà S‚ÇÅ :=
  Set.mem_prod

@[simp]
theorem prod_top : (prod ‚ä§ ‚ä§ : NonUnitalStarSubalgebra R (A √ó B)) = ‚ä§ := by ext; simp
                                                                            -- ‚ä¢ x‚úù ‚àà prod ‚ä§ ‚ä§ ‚Üî x‚úù ‚àà ‚ä§
                                                                                 -- üéâ no goals

theorem prod_mono {S T : NonUnitalStarSubalgebra R A} {S‚ÇÅ T‚ÇÅ : NonUnitalStarSubalgebra R B} :
    S ‚â§ T ‚Üí S‚ÇÅ ‚â§ T‚ÇÅ ‚Üí prod S S‚ÇÅ ‚â§ prod T T‚ÇÅ :=
  Set.prod_mono

@[simp]
theorem prod_inf_prod {S T : NonUnitalStarSubalgebra R A} {S‚ÇÅ T‚ÇÅ : NonUnitalStarSubalgebra R B} :
    S.prod S‚ÇÅ ‚äì T.prod T‚ÇÅ = (S ‚äì T).prod (S‚ÇÅ ‚äì T‚ÇÅ) :=
  SetLike.coe_injective Set.prod_inter_prod

end Prod

section iSupLift

variable {Œπ : Type*}

theorem coe_iSup_of_directed [Nonempty Œπ] {S : Œπ ‚Üí NonUnitalStarSubalgebra R A}
    (dir : Directed (¬∑ ‚â§ ¬∑) S) : ‚Üë(iSup S) = ‚ãÉ i, (S i : Set A) :=
  let K : NonUnitalStarSubalgebra R A :=
    { carrier := ‚ãÉ i, S i
      zero_mem' :=
        let i := @Nonempty.some Œπ inferInstance
        Set.mem_iUnion.2 ‚ü®i, zero_mem (S i)‚ü©
      mul_mem' := fun hx hy =>
        let ‚ü®i, hi‚ü© := Set.mem_iUnion.1 hx
        let ‚ü®j, hj‚ü© := Set.mem_iUnion.1 hy
        let ‚ü®k, hik, hjk‚ü© := dir i j
        Set.mem_iUnion.2 ‚ü®k, mul_mem (s := S k) (hik hi) (hjk hj)‚ü©
      add_mem' := fun hx hy =>
        let ‚ü®i, hi‚ü© := Set.mem_iUnion.1 hx
        let ‚ü®j, hj‚ü© := Set.mem_iUnion.1 hy
        let ‚ü®k, hik, hjk‚ü© := dir i j
        Set.mem_iUnion.2 ‚ü®k, add_mem (s := S k) (hik hi) (hjk hj)‚ü©
      smul_mem' := fun r _x hx =>
        let ‚ü®i, hi‚ü© := Set.mem_iUnion.1 hx
        Set.mem_iUnion.2 ‚ü®i, SMulMemClass.smul_mem (s := (S i)) r hi‚ü©
      star_mem' := fun hx =>
        let ‚ü®i, hi‚ü© := Set.mem_iUnion.1 hx
        Set.mem_iUnion.2 ‚ü®i, star_mem (s := (S i)) hi‚ü© }
  have : iSup S = K := le_antisymm (iSup_le fun i => Set.subset_iUnion (fun i => (S i : Set A)) i)
    (SetLike.coe_subset_coe.1 (Set.iUnion_subset fun _i => SetLike.coe_subset_coe.2 (le_iSup _ _)))
  this.symm ‚ñ∏ rfl

/-- Define a non-unital star algebra homomorphism on a directed supremum of non-unital star
subalgebras by defining it on each non-unital star subalgebra, and proving that it agrees on the
intersection of non-unital star subalgebras. -/
noncomputable def iSupLift [Nonempty Œπ] (K : Œπ ‚Üí NonUnitalStarSubalgebra R A)
    (dir : Directed (¬∑ ‚â§ ¬∑) K) (f : ‚àÄ i, K i ‚Üí‚ãÜ‚Çô‚Çê[R] B)
    (hf : ‚àÄ (i j : Œπ) (h : K i ‚â§ K j), f i = (f j).comp (inclusion h))
    (T : NonUnitalStarSubalgebra R A) (hT : T = iSup K) : ‚Ü•T ‚Üí‚ãÜ‚Çô‚Çê[R] B := by
  subst hT
  -- ‚ä¢ { x // x ‚àà iSup K } ‚Üí‚ãÜ‚Çô‚Çê[R] B
  exact
    { toFun :=
        Set.iUnionLift (fun i => ‚Üë(K i)) (fun i x => f i x)
          (fun i j x hxi hxj => by
            let ‚ü®k, hik, hjk‚ü© := dir i j
            simp only
            rw [hf i k hik, hf j k hjk]
            rfl)
          (‚Üë(iSup K)) (by rw [coe_iSup_of_directed dir])
      map_zero' := by
        dsimp only [SetLike.coe_sort_coe, NonUnitalAlgHom.coe_comp, Function.comp_apply,
          inclusion_mk, Eq.ndrec, id_eq, eq_mpr_eq_cast]
        exact Set.iUnionLift_const _ (fun i : Œπ => (0 : K i)) (fun _ => rfl)  _ (by simp)
      map_mul' := by
        dsimp only [SetLike.coe_sort_coe, NonUnitalAlgHom.coe_comp, Function.comp_apply,
          inclusion_mk, Eq.ndrec, id_eq, eq_mpr_eq_cast, ZeroMemClass.coe_zero,
          AddSubmonoid.mk_add_mk, Set.inclusion_mk]
        apply Set.iUnionLift_binary (coe_iSup_of_directed dir) dir _ (fun _ => (¬∑ * ¬∑))
        on_goal 3 => rw [coe_iSup_of_directed dir]
        all_goals simp
      map_add' := by
        dsimp only [SetLike.coe_sort_coe, NonUnitalAlgHom.coe_comp, Function.comp_apply,
          inclusion_mk, Eq.ndrec, id_eq, eq_mpr_eq_cast]
        apply Set.iUnionLift_binary (coe_iSup_of_directed dir) dir _ (fun _ => (¬∑ + ¬∑))
        on_goal 3 => rw [coe_iSup_of_directed dir]
        all_goals simp
      map_smul' := fun r => by
        dsimp only [SetLike.coe_sort_coe, NonUnitalAlgHom.coe_comp, Function.comp_apply,
          inclusion_mk, Eq.ndrec, id_eq, eq_mpr_eq_cast]
        apply Set.iUnionLift_unary (coe_iSup_of_directed dir) _ (fun _ x => r ‚Ä¢ x)
          (fun _ _ => rfl)
        on_goal 2 => rw [coe_iSup_of_directed dir]
        all_goals simp
      map_star' := by
        dsimp only [SetLike.coe_sort_coe, NonUnitalStarAlgHom.comp_apply, inclusion_mk, Eq.ndrec,
          id_eq, eq_mpr_eq_cast, ZeroMemClass.coe_zero, AddSubmonoid.mk_add_mk, Set.inclusion_mk,
          MulMemClass.mk_mul_mk, NonUnitalAlgHom.toDistribMulActionHom_eq_coe,
          DistribMulActionHom.toFun_eq_coe, NonUnitalAlgHom.coe_to_distribMulActionHom,
          NonUnitalAlgHom.coe_mk]
        apply Set.iUnionLift_unary (coe_iSup_of_directed dir) _ (fun _ x => star x)
          (fun _ _ => rfl)
        on_goal 2 => rw [coe_iSup_of_directed dir]
        all_goals simp [map_star] }

variable [Nonempty Œπ] {K : Œπ ‚Üí NonUnitalStarSubalgebra R A} {dir : Directed (¬∑ ‚â§ ¬∑) K}
  {f : ‚àÄ i, K i ‚Üí‚ãÜ‚Çô‚Çê[R] B} {hf : ‚àÄ (i j : Œπ) (h : K i ‚â§ K j), f i = (f j).comp (inclusion h)}
  {T : NonUnitalStarSubalgebra R A} {hT : T = iSup K}

@[simp]
theorem iSupLift_inclusion {i : Œπ} (x : K i) (h : K i ‚â§ T) :
    iSupLift K dir f hf T hT (inclusion h x) = f i x := by
  subst T
  -- ‚ä¢ ‚Üë(iSupLift K dir f hf (iSup K) (_ : iSup K = iSup K)) (‚Üë(inclusion h) x) = ‚Üë ‚Ä¶
  dsimp [iSupLift]
  -- ‚ä¢ Set.iUnionLift (fun i => ‚Üë(K i)) (fun i x => ‚Üë(f i) x) (_ : ‚àÄ (i j : Œπ) (x : ‚Ä¶
  apply Set.iUnionLift_inclusion
  -- ‚ä¢ ‚Üë(K i) ‚äÜ ‚Üë(iSup K)
  exact h
  -- üéâ no goals

@[simp]
theorem iSupLift_comp_inclusion {i : Œπ} (h : K i ‚â§ T) :
    (iSupLift K dir f hf T hT).comp (inclusion h) = f i := by ext; simp
                                                              -- ‚ä¢ ‚Üë(NonUnitalStarAlgHom.comp (iSupLift K dir f hf T hT) (inclusion h)) x‚úù = ‚Üë( ‚Ä¶
                                                                   -- üéâ no goals

@[simp]
theorem iSupLift_mk {i : Œπ} (x : K i) (hx : (x : A) ‚àà T) :
    iSupLift K dir f hf T hT ‚ü®x, hx‚ü© = f i x := by
  subst hT
  -- ‚ä¢ ‚Üë(iSupLift K dir f hf (iSup K) (_ : iSup K = iSup K)) { val := ‚Üëx, property  ‚Ä¶
  dsimp [iSupLift]
  -- ‚ä¢ Set.iUnionLift (fun i => ‚Üë(K i)) (fun i x => ‚Üë(f i) x) (_ : ‚àÄ (i j : Œπ) (x : ‚Ä¶
  apply Set.iUnionLift_mk
  -- üéâ no goals

theorem iSupLift_of_mem {i : Œπ} (x : T) (hx : (x : A) ‚àà K i) :
    iSupLift K dir f hf T hT x = f i ‚ü®x, hx‚ü© := by
  subst hT
  -- ‚ä¢ ‚Üë(iSupLift K dir f hf (iSup K) (_ : iSup K = iSup K)) x = ‚Üë(f i) { val := ‚Üëx ‚Ä¶
  dsimp [iSupLift]
  -- ‚ä¢ Set.iUnionLift (fun i => ‚Üë(K i)) (fun i x => ‚Üë(f i) x) (_ : ‚àÄ (i j : Œπ) (x : ‚Ä¶
  apply Set.iUnionLift_of_mem
  -- üéâ no goals

end iSupLift

section Center

variable (R A)

/-- The center of a non-unital star algebra is the set of elements which commute with every element.
They form a non-unital star subalgebra. -/
def center : NonUnitalStarSubalgebra R A where
  toNonUnitalSubalgebra := NonUnitalSubalgebra.center R A
  star_mem' := Set.star_mem_center

theorem coe_center : (center R A : Set A) = Set.center A :=
  rfl

@[simp]
theorem center_toNonUnitalSubalgebra :
    (center R A).toNonUnitalSubalgebra = NonUnitalSubalgebra.center R A :=
  rfl

@[simp]
theorem center_eq_top (A : Type*) [NonUnitalCommSemiring A] [StarRing A] [Module R A]
    [IsScalarTower R A A] [SMulCommClass R A A] [StarModule R A] : center R A = ‚ä§ :=
  SetLike.coe_injective (Set.center_eq_univ A)

variable {R A}

instance instNonUnitalCommSemiring : NonUnitalCommSemiring (center R A) :=
  NonUnitalSubalgebra.center.instNonUnitalCommSemiring

instance instNonUnitalCommRing {A : Type*} [NonUnitalRing A] [StarRing A] [Module R A]
    [IsScalarTower R A A] [SMulCommClass R A A] : NonUnitalCommRing (center R A) :=
  NonUnitalSubalgebra.center.instNonUnitalCommRing

theorem mem_center_iff {a : A} : a ‚àà center R A ‚Üî ‚àÄ b : A, b * a = a * b :=
  Iff.rfl

end Center

section Centralizer

variable (R)

/-- The centralizer of the star-closure of a set as a non-unital star subalgebra. -/
def centralizer (s : Set A) : NonUnitalStarSubalgebra R A :=
  { NonUnitalSubalgebra.centralizer R (s ‚à™ star s) with
    star_mem' := Set.star_mem_centralizer }

@[simp, norm_cast]
theorem coe_centralizer (s : Set A) : (centralizer R s : Set A) = (s ‚à™ star s).centralizer :=
  rfl

theorem mem_centralizer_iff {s : Set A} {z : A} :
    z ‚àà centralizer R s ‚Üî ‚àÄ g ‚àà s, g * z = z * g ‚àß star g * z = z * star g := by
  show (‚àÄ g ‚àà s ‚à™ star s, g * z = z * g) ‚Üî ‚àÄ g ‚àà s, g * z = z * g ‚àß star g * z = z * star g
  -- ‚ä¢ (‚àÄ (g : A), g ‚àà s ‚à™ star s ‚Üí g * z = z * g) ‚Üî ‚àÄ (g : A), g ‚àà s ‚Üí g * z = z * ‚Ä¶
  simp only [Set.mem_union, or_imp, forall_and, and_congr_right_iff]
  -- ‚ä¢ (‚àÄ (x : A), x ‚àà s ‚Üí x * z = z * x) ‚Üí ((‚àÄ (x : A), x ‚àà star s ‚Üí x * z = z * x ‚Ä¶
  exact fun _ =>
    ‚ü®fun hz a ha => hz _ (Set.star_mem_star.mpr ha), fun hz a ha => star_star a ‚ñ∏ hz _ ha‚ü©

theorem centralizer_le (s t : Set A) (h : s ‚äÜ t) : centralizer R t ‚â§ centralizer R s :=
  Set.centralizer_subset (Set.union_subset_union h <| Set.preimage_mono h)

@[simp]
theorem centralizer_univ : centralizer R Set.univ = center R A :=
  SetLike.ext' <| by rw [coe_centralizer, Set.univ_union, coe_center, Set.centralizer_univ]
                     -- üéâ no goals

end Centralizer

end NonUnitalStarSubalgebra
