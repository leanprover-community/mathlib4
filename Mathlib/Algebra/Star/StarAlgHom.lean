/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Algebra.Algebra.Equiv
import Mathlib.Algebra.Algebra.Prod
import Mathlib.Algebra.Hom.NonUnitalAlg
import Mathlib.Algebra.Star.Prod

#align_import algebra.star.star_alg_hom from "leanprover-community/mathlib"@"35882ddc66524b6980532a123a4ad4166db34c81"

/-!
# Morphisms of star algebras

This file defines morphisms between `R`-algebras (unital or non-unital) `A` and `B` where both
`A` and `B` are equipped with a `star` operation. These morphisms, namely `StarAlgHom` and
`NonUnitalStarAlgHom` are direct extensions of their non-`star`red counterparts with a field
`map_star` which guarantees they preserve the star operation. We keep the type classes as generic
as possible, in keeping with the definition of `NonUnitalAlgHom` in the non-unital case. In this
file, we only assume `Star` unless we want to talk about the zero map as a
`NonUnitalStarAlgHom`, in which case we need `StarAddMonoid`. Note that the scalar ring `R`
is not required to have a star operation, nor do we need `StarRing` or `StarModule` structures on
`A` and `B`.

As with `NonUnitalAlgHom`, in the non-unital case the multiplications are not assumed to be
associative or unital, or even to be compatible with the scalar actions. In a typical application,
the operations will satisfy compatibility conditions making them into algebras (albeit possibly
non-associative and/or non-unital) but such conditions are not required here for the definitions.

The primary impetus for defining these types is that they constitute the morphisms in the categories
of unital Câ‹†-algebras (with `StarAlgHom`s) and of Câ‹†-algebras (with `NonUnitalStarAlgHom`s).

## Main definitions

  * `NonUnitalStarAlgHom`
  * `StarAlgHom`

## Tags

non-unital, algebra, morphism, star
-/

open EquivLike

/-! ### Non-unital star algebra homomorphisms -/


/-- A *non-unital â‹†-algebra homomorphism* is a non-unital algebra homomorphism between
non-unital `R`-algebras `A` and `B` equipped with a `star` operation, and this homomorphism is
also `star`-preserving. -/
structure NonUnitalStarAlgHom (R A B : Type*) [Monoid R] [NonUnitalNonAssocSemiring A]
  [DistribMulAction R A] [Star A] [NonUnitalNonAssocSemiring B] [DistribMulAction R B]
  [Star B] extends A â†’â‚™â‚[R] B where
  /-- By definition, a non-unital â‹†-algebra homomorphism preserves the `star` operation. -/
  map_star' : âˆ€ a : A, toFun (star a) = star (toFun a)
#align non_unital_star_alg_hom NonUnitalStarAlgHom

@[inherit_doc NonUnitalStarAlgHom] infixr:25 " â†’â‹†â‚™â‚ " => NonUnitalStarAlgHom _

@[inherit_doc] notation:25 A " â†’â‹†â‚™â‚[" R "] " B => NonUnitalStarAlgHom R A B

/-- Reinterpret a non-unital star algebra homomorphism as a non-unital algebra homomorphism
by forgetting the interaction with the star operation. -/
add_decl_doc NonUnitalStarAlgHom.toNonUnitalAlgHom

/-- `NonUnitalStarAlgHomClass F R A B` asserts `F` is a type of bundled non-unital â‹†-algebra
homomorphisms from `A` to `B`. -/
class NonUnitalStarAlgHomClass (F : Type*) (R : outParam (Type*)) (A : outParam (Type*))
  (B : outParam (Type*)) [Monoid R] [Star A] [Star B] [NonUnitalNonAssocSemiring A]
  [NonUnitalNonAssocSemiring B] [DistribMulAction R A] [DistribMulAction R B] extends
  NonUnitalAlgHomClass F R A B, StarHomClass F A B
#align non_unital_star_alg_hom_class NonUnitalStarAlgHomClass

-- Porting note: no longer needed
---- `R` becomes a metavariable but that's fine because it's an `outParam`
--attribute [nolint dangerousInstance] NonUnitalStarAlgHomClass.toStarHomClass

namespace NonUnitalStarAlgHomClass

variable {F R A B : Type*} [Monoid R]

variable [NonUnitalNonAssocSemiring A] [DistribMulAction R A] [Star A]

variable [NonUnitalNonAssocSemiring B] [DistribMulAction R B] [Star B]

/-- Turn an element of a type `F` satisfying `NonUnitalStarAlgHomClass F R A B` into an actual
`NonUnitalStarAlgHom`. This is declared as the default coercion from `F` to `A â†’â‹†â‚™â‚[R] B`. -/
@[coe]
def toNonUnitalStarAlgHom [NonUnitalStarAlgHomClass F R A B] (f : F) : A â†’â‹†â‚™â‚[R] B :=
  { (f : A â†’â‚™â‚[R] B) with
    map_star' := map_star f }

instance [NonUnitalStarAlgHomClass F R A B] : CoeTC F (A â†’â‹†â‚™â‚[R] B) :=
  âŸ¨toNonUnitalStarAlgHomâŸ©

end NonUnitalStarAlgHomClass

namespace NonUnitalStarAlgHom

section Basic

variable {R A B C D : Type*} [Monoid R]

variable [NonUnitalNonAssocSemiring A] [DistribMulAction R A] [Star A]

variable [NonUnitalNonAssocSemiring B] [DistribMulAction R B] [Star B]

variable [NonUnitalNonAssocSemiring C] [DistribMulAction R C] [Star C]

variable [NonUnitalNonAssocSemiring D] [DistribMulAction R D] [Star D]

instance : NonUnitalStarAlgHomClass (A â†’â‹†â‚™â‚[R] B) R A B
    where
  coe f := f.toFun
  coe_injective' := by rintro âŸ¨âŸ¨âŸ¨âŸ¨f, _âŸ©, _âŸ©, _âŸ©, _âŸ© âŸ¨âŸ¨âŸ¨âŸ¨g, _âŸ©, _âŸ©, _âŸ©, _âŸ© h; congr
                       -- âŠ¢ { toNonUnitalAlgHom := { toDistribMulActionHom := { toMulActionHom := { toFu â€¦
                                                                             -- ğŸ‰ no goals
  map_smul f := f.map_smul'
  map_add f := f.map_add'
  map_zero f := f.map_zero'
  map_mul f := f.map_mul'
  map_star f := f.map_star'

-- Porting note: this is no longer useful
--/-- Helper instance for when there's too many metavariables to apply `FunLike.CoeFun`
--directly. -/
--instance : CoeFun (A â†’â‹†â‚™â‚[R] B) fun _ => A â†’ B :=
--  FunLike.hasCoeToFun

-- Porting note: in mathlib3 we didn't need the `Simps.apply` hint.
/-- See Note [custom simps projection] -/
def Simps.apply (f : A â†’â‹†â‚™â‚[R] B) : A â†’ B := f

initialize_simps_projections NonUnitalStarAlgHom
  (toFun â†’ apply)

@[simp]
protected theorem coe_coe {F : Type*} [NonUnitalStarAlgHomClass F R A B] (f : F) :
  â‡‘(f : A â†’â‹†â‚™â‚[R] B) = f := rfl
#align non_unital_star_alg_hom.coe_coe NonUnitalStarAlgHom.coe_coe

@[simp]
theorem coe_toNonUnitalAlgHom {f : A â†’â‹†â‚™â‚[R] B} : (f.toNonUnitalAlgHom : A â†’ B) = f :=
  rfl
#align non_unital_star_alg_hom.coe_to_non_unital_alg_hom NonUnitalStarAlgHom.coe_toNonUnitalAlgHom

@[ext]
theorem ext {f g : A â†’â‹†â‚™â‚[R] B} (h : âˆ€ x, f x = g x) : f = g :=
  FunLike.ext _ _ h
#align non_unital_star_alg_hom.ext NonUnitalStarAlgHom.ext

/-- Copy of a `NonUnitalStarAlgHom` with a new `toFun` equal to the old one. Useful
to fix definitional equalities. -/
protected def copy (f : A â†’â‹†â‚™â‚[R] B) (f' : A â†’ B) (h : f' = f) : A â†’â‹†â‚™â‚[R] B
    where
  toFun := f'
  map_smul' := h.symm â–¸ map_smul f
  map_zero' := h.symm â–¸ map_zero f
  map_add' := h.symm â–¸ map_add f
  map_mul' := h.symm â–¸ map_mul f
  map_star' := h.symm â–¸ map_star f
#align non_unital_star_alg_hom.copy NonUnitalStarAlgHom.copy

@[simp]
theorem coe_copy (f : A â†’â‹†â‚™â‚[R] B) (f' : A â†’ B) (h : f' = f) : â‡‘(f.copy f' h) = f' :=
  rfl
#align non_unital_star_alg_hom.coe_copy NonUnitalStarAlgHom.coe_copy

theorem copy_eq (f : A â†’â‹†â‚™â‚[R] B) (f' : A â†’ B) (h : f' = f) : f.copy f' h = f :=
  FunLike.ext' h
#align non_unital_star_alg_hom.copy_eq NonUnitalStarAlgHom.copy_eq

-- porting note: doesn't align with Mathlib 3 because `NonUnitalStarAlgHom.mk` has a new signature
@[simp]
theorem coe_mk (f : A â†’ B) (hâ‚ hâ‚‚ hâ‚ƒ hâ‚„ hâ‚…) :
    ((âŸ¨âŸ¨âŸ¨âŸ¨f, hâ‚âŸ©, hâ‚‚, hâ‚ƒâŸ©, hâ‚„âŸ©, hâ‚…âŸ© : A â†’â‹†â‚™â‚[R] B) : A â†’ B) = f :=
  rfl
#align non_unital_star_alg_hom.coe_mk NonUnitalStarAlgHom.coe_mkâ‚“

-- this is probably the more useful lemma for Lean 4 and should likely replace `coe_mk` above
@[simp]
theorem coe_mk' (f : A â†’â‚™â‚[R] B) (h) :
    ((âŸ¨f, hâŸ© : A â†’â‹†â‚™â‚[R] B) : A â†’ B) = f :=
  rfl

-- porting note: doesn't align with Mathlib 3 because `NonUnitalStarAlgHom.mk` has a new signature
@[simp]
theorem mk_coe (f : A â†’â‹†â‚™â‚[R] B) (hâ‚ hâ‚‚ hâ‚ƒ hâ‚„ hâ‚…) :
  (âŸ¨âŸ¨âŸ¨âŸ¨f, hâ‚âŸ©, hâ‚‚, hâ‚ƒâŸ©, hâ‚„âŸ©, hâ‚…âŸ© : A â†’â‹†â‚™â‚[R] B) = f := by
  ext
  -- âŠ¢ â†‘{ toNonUnitalAlgHom := { toDistribMulActionHom := { toMulActionHom := { toF â€¦
  rfl
  -- ğŸ‰ no goals
#align non_unital_star_alg_hom.mk_coe NonUnitalStarAlgHom.mk_coeâ‚“

section

variable (R A)

/-- The identity as a non-unital â‹†-algebra homomorphism. -/
protected def id : A â†’â‹†â‚™â‚[R] A :=
  { (1 : A â†’â‚™â‚[R] A) with map_star' := fun _ => rfl }
#align non_unital_star_alg_hom.id NonUnitalStarAlgHom.id

@[simp]
theorem coe_id : â‡‘(NonUnitalStarAlgHom.id R A) = id :=
  rfl
#align non_unital_star_alg_hom.coe_id NonUnitalStarAlgHom.coe_id

end

/-- The composition of non-unital â‹†-algebra homomorphisms, as a non-unital â‹†-algebra
homomorphism. -/
def comp (f : B â†’â‹†â‚™â‚[R] C) (g : A â†’â‹†â‚™â‚[R] B) : A â†’â‹†â‚™â‚[R] C :=
  { f.toNonUnitalAlgHom.comp g.toNonUnitalAlgHom with
    map_star' := by
      simp only [map_star, NonUnitalAlgHom.toFun_eq_coe, eq_self_iff_true, NonUnitalAlgHom.coe_comp,
        coe_toNonUnitalAlgHom, Function.comp_apply, forall_const] }
#align non_unital_star_alg_hom.comp NonUnitalStarAlgHom.comp

@[simp]
theorem coe_comp (f : B â†’â‹†â‚™â‚[R] C) (g : A â†’â‹†â‚™â‚[R] B) : â‡‘(comp f g) = f âˆ˜ g :=
  rfl
#align non_unital_star_alg_hom.coe_comp NonUnitalStarAlgHom.coe_comp

@[simp]
theorem comp_apply (f : B â†’â‹†â‚™â‚[R] C) (g : A â†’â‹†â‚™â‚[R] B) (a : A) : comp f g a = f (g a) :=
  rfl
#align non_unital_star_alg_hom.comp_apply NonUnitalStarAlgHom.comp_apply

@[simp]
theorem comp_assoc (f : C â†’â‹†â‚™â‚[R] D) (g : B â†’â‹†â‚™â‚[R] C) (h : A â†’â‹†â‚™â‚[R] B) :
    (f.comp g).comp h = f.comp (g.comp h) :=
  rfl
#align non_unital_star_alg_hom.comp_assoc NonUnitalStarAlgHom.comp_assoc

@[simp]
theorem id_comp (f : A â†’â‹†â‚™â‚[R] B) : (NonUnitalStarAlgHom.id _ _).comp f = f :=
  ext fun _ => rfl
#align non_unital_star_alg_hom.id_comp NonUnitalStarAlgHom.id_comp

@[simp]
theorem comp_id (f : A â†’â‹†â‚™â‚[R] B) : f.comp (NonUnitalStarAlgHom.id _ _) = f :=
  ext fun _ => rfl
#align non_unital_star_alg_hom.comp_id NonUnitalStarAlgHom.comp_id

instance : Monoid (A â†’â‹†â‚™â‚[R] A) where
  mul := comp
  mul_assoc := comp_assoc
  one := NonUnitalStarAlgHom.id R A
  one_mul := id_comp
  mul_one := comp_id

@[simp]
theorem coe_one : ((1 : A â†’â‹†â‚™â‚[R] A) : A â†’ A) = id :=
  rfl
#align non_unital_star_alg_hom.coe_one NonUnitalStarAlgHom.coe_one

theorem one_apply (a : A) : (1 : A â†’â‹†â‚™â‚[R] A) a = a :=
  rfl
#align non_unital_star_alg_hom.one_apply NonUnitalStarAlgHom.one_apply

end Basic

section Zero

-- the `zero` requires extra type class assumptions because we need `star_zero`
variable {R A B C D : Type*} [Monoid R]

variable [NonUnitalNonAssocSemiring A] [DistribMulAction R A] [StarAddMonoid A]

variable [NonUnitalNonAssocSemiring B] [DistribMulAction R B] [StarAddMonoid B]

instance : Zero (A â†’â‹†â‚™â‚[R] B) :=
  âŸ¨{ (0 : NonUnitalAlgHom R A B) with map_star' := by simp }âŸ©
                                                      -- ğŸ‰ no goals

instance : Inhabited (A â†’â‹†â‚™â‚[R] B) :=
  âŸ¨0âŸ©

instance : MonoidWithZero (A â†’â‹†â‚™â‚[R] A) :=
  { inferInstanceAs (Monoid (A â†’â‹†â‚™â‚[R] A)),
    inferInstanceAs (Zero (A â†’â‹†â‚™â‚[R] A)) with
    zero_mul := fun _ => ext fun _ => rfl
    mul_zero := fun f => ext fun _ => map_zero f }

@[simp]
theorem coe_zero : ((0 : A â†’â‹†â‚™â‚[R] B) : A â†’ B) = 0 :=
  rfl
#align non_unital_star_alg_hom.coe_zero NonUnitalStarAlgHom.coe_zero

theorem zero_apply (a : A) : (0 : A â†’â‹†â‚™â‚[R] B) a = 0 :=
  rfl
#align non_unital_star_alg_hom.zero_apply NonUnitalStarAlgHom.zero_apply

end Zero

end NonUnitalStarAlgHom

/-! ### Unital star algebra homomorphisms -/


section Unital

/-- A *â‹†-algebra homomorphism* is an algebra homomorphism between `R`-algebras `A` and `B`
equipped with a `star` operation, and this homomorphism is also `star`-preserving. -/
structure StarAlgHom (R A B : Type*) [CommSemiring R] [Semiring A] [Algebra R A] [Star A]
  [Semiring B] [Algebra R B] [Star B] extends AlgHom R A B where
  /-- By definition, a â‹†-algebra homomorphism preserves the `star` operation. -/
  map_star' : âˆ€ x : A, toFun (star x) = star (toFun x)
#align star_alg_hom StarAlgHom

@[inherit_doc StarAlgHom] infixr:25 " â†’â‹†â‚ " => StarAlgHom _

@[inherit_doc] notation:25 A " â†’â‹†â‚[" R "] " B => StarAlgHom R A B

/-- Reinterpret a unital star algebra homomorphism as a unital algebra homomorphism
by forgetting the interaction with the star operation. -/
add_decl_doc StarAlgHom.toAlgHom

/-- `StarAlgHomClass F R A B` states that `F` is a type of â‹†-algebra homomorphisms.

You should also extend this typeclass when you extend `StarAlgHom`. -/
class StarAlgHomClass (F : Type*) (R : outParam (Type*)) (A : outParam (Type*))
  (B : outParam (Type*)) [CommSemiring R] [Semiring A] [Algebra R A] [Star A] [Semiring B]
  [Algebra R B] [Star B] extends AlgHomClass F R A B, StarHomClass F A B
#align star_alg_hom_class StarAlgHomClass

-- Porting note: no longer needed
---- `R` becomes a metavariable but that's fine because it's an `outParam`
--attribute [nolint dangerousInstance] StarAlgHomClass.toStarHomClass

namespace StarAlgHomClass

variable (F R A B : Type*)

-- See note [lower instance priority]
instance (priority := 100) toNonUnitalStarAlgHomClass [CommSemiring R] [Semiring A]
  [Algebra R A] [Star A] [Semiring B] [Algebra R B] [Star B]
  [StarAlgHomClass F R A B] :
  NonUnitalStarAlgHomClass F R A B :=
  { StarAlgHomClass.toAlgHomClass, StarAlgHomClass.toStarHomClass R with
    map_smul := map_smul }
#align star_alg_hom_class.to_non_unital_star_alg_hom_class StarAlgHomClass.toNonUnitalStarAlgHomClass

variable [CommSemiring R] [Semiring A] [Algebra R A] [Star A]

variable [Semiring B] [Algebra R B] [Star B] [hF : StarAlgHomClass F R A B]

variable {F R A B} in
/-- Turn an element of a type `F` satisfying `StarAlgHomClass F R A B` into an actual
`StarAlgHom`. This is declared as the default coercion from `F` to `A â†’â‹†â‚[R] B`. -/
@[coe]
def toStarAlgHom (f : F) : A â†’â‹†â‚[R] B :=
  { (f : A â†’â‚[R] B) with
    map_star' := map_star f }

instance : CoeTC F (A â†’â‹†â‚[R] B) :=
  âŸ¨toStarAlgHomâŸ©

end StarAlgHomClass

namespace StarAlgHom

variable {F R A B C D : Type*} [CommSemiring R] [Semiring A] [Algebra R A] [Star A] [Semiring B]
  [Algebra R B] [Star B] [Semiring C] [Algebra R C] [Star C] [Semiring D] [Algebra R D] [Star D]

instance : StarAlgHomClass (A â†’â‹†â‚[R] B) R A B
    where
  coe f := f.toFun
  coe_injective' := by rintro âŸ¨âŸ¨âŸ¨âŸ¨âŸ¨f, _âŸ©, _âŸ©, _âŸ©, _âŸ©, _âŸ© âŸ¨âŸ¨âŸ¨âŸ¨âŸ¨g, _âŸ©, _âŸ©, _âŸ©, _âŸ©, _âŸ© h; congr
                       -- âŠ¢ { toAlgHom := { toRingHom := { toMonoidHom := { toOneHom := { toFun := f, ma â€¦
                                                                                       -- ğŸ‰ no goals
  map_mul f := f.map_mul'
  map_one f := f.map_one'
  map_add f := f.map_add'
  map_zero f := f.map_zero'
  commutes f := f.commutes'
  map_star f := f.map_star'

@[simp]
protected theorem coe_coe {F : Type*} [StarAlgHomClass F R A B] (f : F) : â‡‘(f : A â†’â‹†â‚[R] B) = f :=
  rfl
#align star_alg_hom.coe_coe StarAlgHom.coe_coe

-- Porting note: in mathlib3 we didn't need the `Simps.apply` hint.
/-- See Note [custom simps projection] -/
def Simps.apply (f : A â†’â‹†â‚[R] B) : A â†’ B := f

initialize_simps_projections StarAlgHom (toFun â†’ apply)

@[simp]
theorem coe_toAlgHom {f : A â†’â‹†â‚[R] B} : (f.toAlgHom : A â†’ B) = f :=
  rfl
#align star_alg_hom.coe_to_alg_hom StarAlgHom.coe_toAlgHom

@[ext]
theorem ext {f g : A â†’â‹†â‚[R] B} (h : âˆ€ x, f x = g x) : f = g :=
  FunLike.ext _ _ h
#align star_alg_hom.ext StarAlgHom.ext

/-- Copy of a `StarAlgHom` with a new `toFun` equal to the old one. Useful
to fix definitional equalities. -/
protected def copy (f : A â†’â‹†â‚[R] B) (f' : A â†’ B) (h : f' = f) : A â†’â‹†â‚[R] B
    where
  toFun := f'
  map_one' := h.symm â–¸ map_one f
  map_mul' := h.symm â–¸ map_mul f
  map_zero' := h.symm â–¸ map_zero f
  map_add' := h.symm â–¸ map_add f
  commutes' := h.symm â–¸ AlgHomClass.commutes f
  map_star' := h.symm â–¸ map_star f
#align star_alg_hom.copy StarAlgHom.copy

@[simp]
theorem coe_copy (f : A â†’â‹†â‚[R] B) (f' : A â†’ B) (h : f' = f) : â‡‘(f.copy f' h) = f' :=
  rfl
#align star_alg_hom.coe_copy StarAlgHom.coe_copy

theorem copy_eq (f : A â†’â‹†â‚[R] B) (f' : A â†’ B) (h : f' = f) : f.copy f' h = f :=
  FunLike.ext' h
#align star_alg_hom.copy_eq StarAlgHom.copy_eq

-- porting note: doesn't align with Mathlib 3 because `StarAlgHom.mk` has a new signature
@[simp]
theorem coe_mk (f : A â†’ B) (hâ‚ hâ‚‚ hâ‚ƒ hâ‚„ hâ‚… hâ‚†) :
    ((âŸ¨âŸ¨âŸ¨âŸ¨âŸ¨f, hâ‚âŸ©, hâ‚‚âŸ©, hâ‚ƒ, hâ‚„âŸ©, hâ‚…âŸ©, hâ‚†âŸ© : A â†’â‹†â‚[R] B) : A â†’ B) = f :=
  rfl
#align star_alg_hom.coe_mk StarAlgHom.coe_mkâ‚“

-- this is probably the more useful lemma for Lean 4 and should likely replace `coe_mk` above
@[simp]
theorem coe_mk' (f : A â†’â‚[R] B) (h) :
    ((âŸ¨f, hâŸ© : A â†’â‹†â‚[R] B) : A â†’ B) = f :=
  rfl

-- porting note: doesn't align with Mathlib 3 because `StarAlgHom.mk` has a new signature
@[simp]
theorem mk_coe (f : A â†’â‹†â‚[R] B) (hâ‚ hâ‚‚ hâ‚ƒ hâ‚„ hâ‚… hâ‚†) :
    (âŸ¨âŸ¨âŸ¨âŸ¨âŸ¨f, hâ‚âŸ©, hâ‚‚âŸ©, hâ‚ƒ, hâ‚„âŸ©, hâ‚…âŸ©, hâ‚†âŸ© : A â†’â‹†â‚[R] B) = f := by
  ext
  -- âŠ¢ â†‘{ toAlgHom := { toRingHom := { toMonoidHom := { toOneHom := { toFun := â†‘f,  â€¦
  rfl
  -- ğŸ‰ no goals
#align star_alg_hom.mk_coe StarAlgHom.mk_coeâ‚“

section

variable (R A)

/-- The identity as a `StarAlgHom`. -/
protected def id : A â†’â‹†â‚[R] A :=
  { AlgHom.id _ _ with map_star' := fun _ => rfl }
#align star_alg_hom.id StarAlgHom.id

@[simp]
theorem coe_id : â‡‘(StarAlgHom.id R A) = id :=
  rfl
#align star_alg_hom.coe_id StarAlgHom.coe_id

end

instance : Inhabited (A â†’â‹†â‚[R] A) :=
  âŸ¨StarAlgHom.id R AâŸ©

/-- The composition of â‹†-algebra homomorphisms, as a â‹†-algebra homomorphism. -/
def comp (f : B â†’â‹†â‚[R] C) (g : A â†’â‹†â‚[R] B) : A â†’â‹†â‚[R] C :=
  { f.toAlgHom.comp g.toAlgHom with
    map_star' := by
      simp only [map_star, AlgHom.toFun_eq_coe, AlgHom.coe_comp, coe_toAlgHom,
        Function.comp_apply, eq_self_iff_true, forall_const] }
#align star_alg_hom.comp StarAlgHom.comp

@[simp]
theorem coe_comp (f : B â†’â‹†â‚[R] C) (g : A â†’â‹†â‚[R] B) : â‡‘(comp f g) = f âˆ˜ g :=
  rfl
#align star_alg_hom.coe_comp StarAlgHom.coe_comp

@[simp]
theorem comp_apply (f : B â†’â‹†â‚[R] C) (g : A â†’â‹†â‚[R] B) (a : A) : comp f g a = f (g a) :=
  rfl
#align star_alg_hom.comp_apply StarAlgHom.comp_apply

@[simp]
theorem comp_assoc (f : C â†’â‹†â‚[R] D) (g : B â†’â‹†â‚[R] C) (h : A â†’â‹†â‚[R] B) :
    (f.comp g).comp h = f.comp (g.comp h) :=
  rfl
#align star_alg_hom.comp_assoc StarAlgHom.comp_assoc

@[simp]
theorem id_comp (f : A â†’â‹†â‚[R] B) : (StarAlgHom.id _ _).comp f = f :=
  ext fun _ => rfl
#align star_alg_hom.id_comp StarAlgHom.id_comp

@[simp]
theorem comp_id (f : A â†’â‹†â‚[R] B) : f.comp (StarAlgHom.id _ _) = f :=
  ext fun _ => rfl
#align star_alg_hom.comp_id StarAlgHom.comp_id

instance : Monoid (A â†’â‹†â‚[R] A) where
  mul := comp
  mul_assoc := comp_assoc
  one := StarAlgHom.id R A
  one_mul := id_comp
  mul_one := comp_id

/-- A unital morphism of â‹†-algebras is a `NonUnitalStarAlgHom`. -/
def toNonUnitalStarAlgHom (f : A â†’â‹†â‚[R] B) : A â†’â‹†â‚™â‚[R] B :=
  { f with map_smul' := map_smul f }
#align star_alg_hom.to_non_unital_star_alg_hom StarAlgHom.toNonUnitalStarAlgHom

@[simp]
theorem coe_toNonUnitalStarAlgHom (f : A â†’â‹†â‚[R] B) : (f.toNonUnitalStarAlgHom : A â†’ B) = f :=
  rfl
#align star_alg_hom.coe_to_non_unital_star_alg_hom StarAlgHom.coe_toNonUnitalStarAlgHom

end StarAlgHom

end Unital

/-! ### Operations on the product type

Note that this is copied from [`Algebra/Hom/NonUnitalAlg`](NonUnitalAlg). -/


namespace NonUnitalStarAlgHom

section Prod

variable (R A B C : Type*) [Monoid R] [NonUnitalNonAssocSemiring A] [DistribMulAction R A] [Star A]
  [NonUnitalNonAssocSemiring B] [DistribMulAction R B] [Star B] [NonUnitalNonAssocSemiring C]
  [DistribMulAction R C] [Star C]

/-- The first projection of a product is a non-unital â‹†-algebra homomoprhism. -/
@[simps!]
def fst : A Ã— B â†’â‹†â‚™â‚[R] A :=
  { NonUnitalAlgHom.fst R A B with map_star' := fun _ => rfl }
#align non_unital_star_alg_hom.fst NonUnitalStarAlgHom.fst

/-- The second projection of a product is a non-unital â‹†-algebra homomorphism. -/
@[simps!]
def snd : A Ã— B â†’â‹†â‚™â‚[R] B :=
  { NonUnitalAlgHom.snd R A B with map_star' := fun _ => rfl }
#align non_unital_star_alg_hom.snd NonUnitalStarAlgHom.snd

variable {R A B C}

/-- The `Pi.prod` of two morphisms is a morphism. -/
@[simps!]
def prod (f : A â†’â‹†â‚™â‚[R] B) (g : A â†’â‹†â‚™â‚[R] C) : A â†’â‹†â‚™â‚[R] B Ã— C :=
  { f.toNonUnitalAlgHom.prod g.toNonUnitalAlgHom with
    map_star' := fun x => by simp [map_star, Prod.star_def] }
                             -- ğŸ‰ no goals
#align non_unital_star_alg_hom.prod NonUnitalStarAlgHom.prod

theorem coe_prod (f : A â†’â‹†â‚™â‚[R] B) (g : A â†’â‹†â‚™â‚[R] C) : â‡‘(f.prod g) = Pi.prod f g :=
  rfl
#align non_unital_star_alg_hom.coe_prod NonUnitalStarAlgHom.coe_prod

@[simp]
theorem fst_prod (f : A â†’â‹†â‚™â‚[R] B) (g : A â†’â‹†â‚™â‚[R] C) : (fst R B C).comp (prod f g) = f := by
  ext; rfl
  -- âŠ¢ â†‘(comp (fst R B C) (prod f g)) xâœ = â†‘f xâœ
       -- ğŸ‰ no goals
#align non_unital_star_alg_hom.fst_prod NonUnitalStarAlgHom.fst_prod

@[simp]
theorem snd_prod (f : A â†’â‹†â‚™â‚[R] B) (g : A â†’â‹†â‚™â‚[R] C) : (snd R B C).comp (prod f g) = g := by
  ext; rfl
  -- âŠ¢ â†‘(comp (snd R B C) (prod f g)) xâœ = â†‘g xâœ
       -- ğŸ‰ no goals
#align non_unital_star_alg_hom.snd_prod NonUnitalStarAlgHom.snd_prod

@[simp]
theorem prod_fst_snd : prod (fst R A B) (snd R A B) = 1 :=
  FunLike.coe_injective Pi.prod_fst_snd
#align non_unital_star_alg_hom.prod_fst_snd NonUnitalStarAlgHom.prod_fst_snd

/-- Taking the product of two maps with the same domain is equivalent to taking the product of
their codomains. -/
@[simps]
def prodEquiv : (A â†’â‹†â‚™â‚[R] B) Ã— (A â†’â‹†â‚™â‚[R] C) â‰ƒ (A â†’â‹†â‚™â‚[R] B Ã— C)
    where
  toFun f := f.1.prod f.2
  invFun f := ((fst _ _ _).comp f, (snd _ _ _).comp f)
  left_inv f := by ext <;> rfl
                   -- âŠ¢ â†‘((fun f => (comp (fst R B C) f, comp (snd R B C) f)) ((fun f => prod f.fst  â€¦
                           -- ğŸ‰ no goals
                           -- ğŸ‰ no goals
  right_inv f := by ext <;> rfl
                    -- âŠ¢ (â†‘((fun f => prod f.fst f.snd) ((fun f => (comp (fst R B C) f, comp (snd R B â€¦
                            -- ğŸ‰ no goals
                            -- ğŸ‰ no goals
#align non_unital_star_alg_hom.prod_equiv NonUnitalStarAlgHom.prodEquiv

end Prod

section InlInr

variable (R A B C : Type*) [Monoid R] [NonUnitalNonAssocSemiring A] [DistribMulAction R A]
  [StarAddMonoid A] [NonUnitalNonAssocSemiring B] [DistribMulAction R B] [StarAddMonoid B]
  [NonUnitalNonAssocSemiring C] [DistribMulAction R C] [StarAddMonoid C]

/-- The left injection into a product is a non-unital algebra homomorphism. -/
def inl : A â†’â‹†â‚™â‚[R] A Ã— B :=
  prod 1 0
#align non_unital_star_alg_hom.inl NonUnitalStarAlgHom.inl

/-- The right injection into a product is a non-unital algebra homomorphism. -/
def inr : B â†’â‹†â‚™â‚[R] A Ã— B :=
  prod 0 1
#align non_unital_star_alg_hom.inr NonUnitalStarAlgHom.inr

variable {R A B}

@[simp]
theorem coe_inl : (inl R A B : A â†’ A Ã— B) = fun x => (x, 0) :=
  rfl
#align non_unital_star_alg_hom.coe_inl NonUnitalStarAlgHom.coe_inl

theorem inl_apply (x : A) : inl R A B x = (x, 0) :=
  rfl
#align non_unital_star_alg_hom.inl_apply NonUnitalStarAlgHom.inl_apply

@[simp]
theorem coe_inr : (inr R A B : B â†’ A Ã— B) = Prod.mk 0 :=
  rfl
#align non_unital_star_alg_hom.coe_inr NonUnitalStarAlgHom.coe_inr

theorem inr_apply (x : B) : inr R A B x = (0, x) :=
  rfl
#align non_unital_star_alg_hom.inr_apply NonUnitalStarAlgHom.inr_apply

end InlInr

end NonUnitalStarAlgHom

namespace StarAlgHom

variable (R A B C : Type*) [CommSemiring R] [Semiring A] [Algebra R A] [Star A] [Semiring B]
  [Algebra R B] [Star B] [Semiring C] [Algebra R C] [Star C]

/-- The first projection of a product is a â‹†-algebra homomoprhism. -/
@[simps!]
def fst : A Ã— B â†’â‹†â‚[R] A :=
  { AlgHom.fst R A B with map_star' := fun _ => rfl }
#align star_alg_hom.fst StarAlgHom.fst

/-- The second projection of a product is a â‹†-algebra homomorphism. -/
@[simps!]
def snd : A Ã— B â†’â‹†â‚[R] B :=
  { AlgHom.snd R A B with map_star' := fun _ => rfl }
#align star_alg_hom.snd StarAlgHom.snd

variable {R A B C}

/-- The `Pi.prod` of two morphisms is a morphism. -/
@[simps!]
def prod (f : A â†’â‹†â‚[R] B) (g : A â†’â‹†â‚[R] C) : A â†’â‹†â‚[R] B Ã— C :=
  { f.toAlgHom.prod g.toAlgHom with map_star' := fun x => by simp [Prod.star_def, map_star] }
                                                             -- ğŸ‰ no goals
#align star_alg_hom.prod StarAlgHom.prod

theorem coe_prod (f : A â†’â‹†â‚[R] B) (g : A â†’â‹†â‚[R] C) : â‡‘(f.prod g) = Pi.prod f g :=
  rfl
#align star_alg_hom.coe_prod StarAlgHom.coe_prod

@[simp]
theorem fst_prod (f : A â†’â‹†â‚[R] B) (g : A â†’â‹†â‚[R] C) : (fst R B C).comp (prod f g) = f := by
  ext; rfl
  -- âŠ¢ â†‘(comp (fst R B C) (prod f g)) xâœ = â†‘f xâœ
       -- ğŸ‰ no goals
#align star_alg_hom.fst_prod StarAlgHom.fst_prod

@[simp]
theorem snd_prod (f : A â†’â‹†â‚[R] B) (g : A â†’â‹†â‚[R] C) : (snd R B C).comp (prod f g) = g := by
  ext; rfl
  -- âŠ¢ â†‘(comp (snd R B C) (prod f g)) xâœ = â†‘g xâœ
       -- ğŸ‰ no goals
#align star_alg_hom.snd_prod StarAlgHom.snd_prod

@[simp]
theorem prod_fst_snd : prod (fst R A B) (snd R A B) = 1 :=
  FunLike.coe_injective Pi.prod_fst_snd
#align star_alg_hom.prod_fst_snd StarAlgHom.prod_fst_snd

/-- Taking the product of two maps with the same domain is equivalent to taking the product of
their codomains. -/
@[simps]
def prodEquiv : (A â†’â‹†â‚[R] B) Ã— (A â†’â‹†â‚[R] C) â‰ƒ (A â†’â‹†â‚[R] B Ã— C)
    where
  toFun f := f.1.prod f.2
  invFun f := ((fst _ _ _).comp f, (snd _ _ _).comp f)
  left_inv f := by ext <;> rfl
                   -- âŠ¢ â†‘((fun f => (comp (fst R B C) f, comp (snd R B C) f)) ((fun f => prod f.fst  â€¦
                           -- ğŸ‰ no goals
                           -- ğŸ‰ no goals
  right_inv f := by ext <;> rfl
                    -- âŠ¢ (â†‘((fun f => prod f.fst f.snd) ((fun f => (comp (fst R B C) f, comp (snd R B â€¦
                            -- ğŸ‰ no goals
                            -- ğŸ‰ no goals
#align star_alg_hom.prod_equiv StarAlgHom.prodEquiv

end StarAlgHom

/-! ### Star algebra equivalences -/

-- Porting note: changed order of arguments to work around
-- [https://github.com/leanprover-community/mathlib4/issues/2505]
/-- A *â‹†-algebra* equivalence is an equivalence preserving addition, multiplication, scalar
multiplication and the star operation, which allows for considering both unital and non-unital
equivalences with a single structure. Currently, `AlgEquiv` requires unital algebras, which is
why this structure does not extend it. -/
structure StarAlgEquiv (R A B : Type*) [Add A] [Add B] [Mul A] [Mul B] [SMul R A] [SMul R B]
  [Star A] [Star B] extends A â‰ƒ+* B where
  /-- By definition, a â‹†-algebra equivalence preserves the `star` operation. -/
  map_star' : âˆ€ a : A, toFun (star a) = star (toFun a)
  /-- By definition, a â‹†-algebra equivalence commutes with the action of scalars. -/
  map_smul' : âˆ€ (r : R) (a : A), toFun (r â€¢ a) = r â€¢ toFun a
#align star_alg_equiv StarAlgEquiv

@[inherit_doc StarAlgEquiv] infixr:25 " â‰ƒâ‹†â‚ " => StarAlgEquiv _

@[inherit_doc] notation:25 A " â‰ƒâ‹†â‚[" R "] " B => StarAlgEquiv R A B

/-- Reinterpret a star algebra equivalence as a `RingEquiv` by forgetting the interaction with
the star operation and scalar multiplication. -/
add_decl_doc StarAlgEquiv.toRingEquiv

/-- `StarAlgEquivClass F R A B` asserts `F` is a type of bundled â‹†-algebra equivalences between
`A` and `B`.

You should also extend this typeclass when you extend `StarAlgEquiv`. -/
class StarAlgEquivClass (F : Type*) (R : outParam (Type*)) (A : outParam (Type*))
  (B : outParam (Type*)) [Add A] [Mul A] [SMul R A] [Star A] [Add B] [Mul B] [SMul R B]
  [Star B] extends RingEquivClass F A B where
  /-- By definition, a â‹†-algebra equivalence preserves the `star` operation. -/
  map_star : âˆ€ (f : F) (a : A), f (star a) = star (f a)
  /-- By definition, a â‹†-algebra equivalence commutes with the action of scalars. -/
  map_smul : âˆ€ (f : F) (r : R) (a : A), f (r â€¢ a) = r â€¢ f a
#align star_alg_equiv_class StarAlgEquivClass

-- Porting note: no longer needed
---- `R` becomes a metavariable but that's fine because it's an `outParam`
-- attribute [nolint dangerousInstance] StarAlgEquivClass.toRingEquivClass

namespace StarAlgEquivClass

-- Porting note: Made following instance non-dangerous through [...] -> [...] replacement
-- See note [lower instance priority]
instance (priority := 50) {F R A B : Type*} [Add A] [Mul A] [SMul R A] [Star A]
    [Add B] [Mul B] [SMul R B] [Star B] [hF : StarAlgEquivClass F R A B] :
    StarHomClass F A B :=
  { hF with
    coe := fun f => f
    coe_injective' := FunLike.coe_injective }

-- Porting note: no longer needed
---- `R` becomes a metavariable but that's fine because it's an `outParam`
-- attribute [nolint dangerousInstance] StarAlgEquivClass.instStarHomClass

-- See note [lower instance priority]
instance (priority := 50) {F R A B : Type*} [Add A] [Mul A] [Star A] [SMul R A]
    [Add B] [Mul B] [SMul R B] [Star B] [hF : StarAlgEquivClass F R A B] :
    SMulHomClass F R A B :=
  { hF with
    coe := fun f => f
    coe_injective' := FunLike.coe_injective }

-- Porting note: no longer needed
---- `R` becomes a metavariable but that's fine because it's an `outParam`
--attribute [nolint dangerous_instance] StarAlgEquivClass.smulHomClass

-- See note [lower instance priority]
instance (priority := 100) {F R A B : Type*} [Monoid R] [NonUnitalNonAssocSemiring A]
    [DistribMulAction R A] [Star A] [NonUnitalNonAssocSemiring B]
    [DistribMulAction R B] [Star B] [hF : StarAlgEquivClass F R A B] :
    NonUnitalStarAlgHomClass F R A B :=
  { hF with
    coe := fun f => f
    coe_injective' := FunLike.coe_injective
    map_zero := map_zero }

-- See note [lower instance priority]
instance (priority := 100) instStarAlgHomClass (F R A B : Type*) [CommSemiring R] [Semiring A]
    [Algebra R A] [Star A] [Semiring B] [Algebra R B] [Star B]
    [hF : StarAlgEquivClass F R A B] : StarAlgHomClass F R A B :=
  { hF with
    coe := fun f => f
    coe_injective' := FunLike.coe_injective
    map_one := map_one
    map_zero := map_zero
    commutes := fun f r => by simp only [Algebra.algebraMap_eq_smul_one, map_smul, map_one] }
                              -- ğŸ‰ no goals

-- See note [lower instance priority]
instance (priority := 100) toAlgEquivClass {F R A B : Type*} [CommSemiring R]
    [Ring A] [Ring B] [Algebra R A] [Algebra R B] [Star A] [Star B] [StarAlgEquivClass F R A B] :
    AlgEquivClass F R A B :=
  { StarAlgEquivClass.toRingEquivClass,
    StarAlgEquivClass.instStarAlgHomClass F R A B with
    coe := fun f => f
    inv := fun f => EquivLike.inv f }

end StarAlgEquivClass

namespace StarAlgEquiv

section Basic

variable {F R A B C : Type*} [Add A] [Add B] [Mul A] [Mul B] [SMul R A] [SMul R B] [Star A]
  [Star B] [Add C] [Mul C] [SMul R C] [Star C]

instance : StarAlgEquivClass (A â‰ƒâ‹†â‚[R] B) R A B
    where
  coe f := f.toFun
  inv f := f.invFun
  left_inv f := f.left_inv
  right_inv f := f.right_inv
  coe_injective' f g hâ‚ hâ‚‚ := by
    rcases f with âŸ¨âŸ¨âŸ¨_, _, _âŸ©, _âŸ©, _âŸ©
    -- âŠ¢ { toRingEquiv := { toEquiv := { toFun := toFunâœ, invFun := invFunâœ, left_inv â€¦
    rcases g with âŸ¨âŸ¨âŸ¨_, _, _âŸ©, _âŸ©, _âŸ©
    -- âŠ¢ { toRingEquiv := { toEquiv := { toFun := toFunâœÂ¹, invFun := invFunâœÂ¹, left_i â€¦
    congr
    -- ğŸ‰ no goals
  map_mul f := f.map_mul'
  map_add f := f.map_add'
  map_star := map_star'
  map_smul := map_smul'

@[simp]
theorem toRingEquiv_eq_coe (e : A â‰ƒâ‹†â‚[R] B) : e.toRingEquiv = e :=
  rfl

-- Porting note: this is no longer useful
--/-- Helper instance for when there's too many metavariables to apply
--`FunLike.CoeFun` directly. -/
--instance : CoeFun (A â‰ƒâ‹†â‚[R] B) fun _ => A â†’ B :=
--  âŸ¨StarAlgEquiv.toFunâŸ©

@[ext]
theorem ext {f g : A â‰ƒâ‹†â‚[R] B} (h : âˆ€ a, f a = g a) : f = g :=
  FunLike.ext f g h
#align star_alg_equiv.ext StarAlgEquiv.ext

theorem ext_iff {f g : A â‰ƒâ‹†â‚[R] B} : f = g â†” âˆ€ a, f a = g a :=
  FunLike.ext_iff
#align star_alg_equiv.ext_iff StarAlgEquiv.ext_iff

/-- Star algebra equivalences are reflexive. -/
@[refl]
def refl : A â‰ƒâ‹†â‚[R] A :=
  { RingEquiv.refl A with
    map_smul' := fun _ _ => rfl
    map_star' := fun _ => rfl }
#align star_alg_equiv.refl StarAlgEquiv.refl

instance : Inhabited (A â‰ƒâ‹†â‚[R] A) :=
  âŸ¨reflâŸ©

@[simp]
theorem coe_refl : â‡‘(refl : A â‰ƒâ‹†â‚[R] A) = id :=
  rfl
#align star_alg_equiv.coe_refl StarAlgEquiv.coe_refl

-- Porting note: changed proof a bit by using `EquivLike` to avoid lots of coercions
/-- Star algebra equivalences are symmetric. -/
@[symm]
nonrec def symm (e : A â‰ƒâ‹†â‚[R] B) : B â‰ƒâ‹†â‚[R] A :=
  { e.symm with
    map_star' := fun b => by
      simpa only [apply_inv_apply, inv_apply_apply] using
        congr_arg (inv e) (map_star e (inv e b)).symm
    map_smul' := fun r b => by
      simpa only [apply_inv_apply, inv_apply_apply] using
        congr_arg (inv e) (map_smul e r (inv e b)).symm }
#align star_alg_equiv.symm StarAlgEquiv.symm

-- Porting note: in mathlib3 we didn't need the `Simps.apply` hint.
/-- See Note [custom simps projection] -/
def Simps.apply (e : A â‰ƒâ‹†â‚[R] B) : A â†’ B := e

/-- See Note [custom simps projection] -/
def Simps.symm_apply (e : A â‰ƒâ‹†â‚[R] B) : B â†’ A :=
  e.symm
#align star_alg_equiv.simps.symm_apply StarAlgEquiv.Simps.symm_apply

initialize_simps_projections StarAlgEquiv (toFun â†’ apply, invFun â†’ symm_apply)

-- Porting note: use `EquivLike.inv` instead of `invFun`
@[simp]
theorem invFun_eq_symm {e : A â‰ƒâ‹†â‚[R] B} : EquivLike.inv e = e.symm :=
  rfl
#align star_alg_equiv.inv_fun_eq_symm StarAlgEquiv.invFun_eq_symm

@[simp]
theorem symm_symm (e : A â‰ƒâ‹†â‚[R] B) : e.symm.symm = e := by
  ext
  -- âŠ¢ â†‘(symm (symm e)) aâœ = â†‘e aâœ
  rfl
  -- ğŸ‰ no goals
#align star_alg_equiv.symm_symm StarAlgEquiv.symm_symm

theorem symm_bijective : Function.Bijective (symm : (A â‰ƒâ‹†â‚[R] B) â†’ B â‰ƒâ‹†â‚[R] A) :=
  Equiv.bijective âŸ¨symm, symm, symm_symm, symm_symmâŸ©
#align star_alg_equiv.symm_bijective StarAlgEquiv.symm_bijective

-- porting note: doesn't align with Mathlib 3 because `StarAlgEquiv.mk` has a new signature
@[simp]
theorem mk_coe' (e : A â‰ƒâ‹†â‚[R] B) (f hâ‚ hâ‚‚ hâ‚ƒ hâ‚„ hâ‚… hâ‚†) :
    (âŸ¨âŸ¨âŸ¨f, e, hâ‚, hâ‚‚âŸ©, hâ‚ƒ, hâ‚„âŸ©, hâ‚…, hâ‚†âŸ© : B â‰ƒâ‹†â‚[R] A) = e.symm :=
  symm_bijective.injective <| ext fun _ => rfl
#align star_alg_equiv.mk_coe' StarAlgEquiv.mk_coe'â‚“

-- porting note: doesn't align with Mathlib 3 because `StarAlgEquiv.mk` has a new signature
@[simp]
theorem symm_mk (f f') (hâ‚ hâ‚‚ hâ‚ƒ hâ‚„ hâ‚… hâ‚†) :
    (âŸ¨âŸ¨âŸ¨f, f', hâ‚, hâ‚‚âŸ©, hâ‚ƒ, hâ‚„âŸ©, hâ‚…, hâ‚†âŸ© : A â‰ƒâ‹†â‚[R] B).symm =
      { (âŸ¨âŸ¨âŸ¨f, f', hâ‚, hâ‚‚âŸ©, hâ‚ƒ, hâ‚„âŸ©, hâ‚…, hâ‚†âŸ© : A â‰ƒâ‹†â‚[R] B).symm with
        toFun := f'
        invFun := f } :=
  rfl
#align star_alg_equiv.symm_mk StarAlgEquiv.symm_mkâ‚“

@[simp]
theorem refl_symm : (StarAlgEquiv.refl : A â‰ƒâ‹†â‚[R] A).symm = StarAlgEquiv.refl :=
  rfl
#align star_alg_equiv.refl_symm StarAlgEquiv.refl_symm

-- should be a `simp` lemma, but causes a linter timeout
theorem to_ringEquiv_symm (f : A â‰ƒâ‹†â‚[R] B) : (f : A â‰ƒ+* B).symm = f.symm :=
  rfl
#align star_alg_equiv.to_ring_equiv_symm StarAlgEquiv.to_ringEquiv_symm

@[simp]
theorem symm_to_ringEquiv (e : A â‰ƒâ‹†â‚[R] B) : (e.symm : B â‰ƒ+* A) = (e : A â‰ƒ+* B).symm :=
  rfl
#align star_alg_equiv.symm_to_ring_equiv StarAlgEquiv.symm_to_ringEquiv

/-- Star algebra equivalences are transitive. -/
@[trans]
def trans (eâ‚ : A â‰ƒâ‹†â‚[R] B) (eâ‚‚ : B â‰ƒâ‹†â‚[R] C) : A â‰ƒâ‹†â‚[R] C :=
  { eâ‚.toRingEquiv.trans
      eâ‚‚.toRingEquiv with
    map_smul' := fun r a =>
      show eâ‚‚.toFun (eâ‚.toFun (r â€¢ a)) = r â€¢ eâ‚‚.toFun (eâ‚.toFun a) by
        rw [eâ‚.map_smul', eâ‚‚.map_smul']
        -- ğŸ‰ no goals
    map_star' := fun a =>
      show eâ‚‚.toFun (eâ‚.toFun (star a)) = star (eâ‚‚.toFun (eâ‚.toFun a)) by
        -- ğŸ‰ no goals
        rw [eâ‚.map_star', eâ‚‚.map_star'] }
#align star_alg_equiv.trans StarAlgEquiv.trans

@[simp]
theorem apply_symm_apply (e : A â‰ƒâ‹†â‚[R] B) : âˆ€ x, e (e.symm x) = x :=
  e.toRingEquiv.apply_symm_apply
#align star_alg_equiv.apply_symm_apply StarAlgEquiv.apply_symm_apply

@[simp]
theorem symm_apply_apply (e : A â‰ƒâ‹†â‚[R] B) : âˆ€ x, e.symm (e x) = x :=
  e.toRingEquiv.symm_apply_apply
#align star_alg_equiv.symm_apply_apply StarAlgEquiv.symm_apply_apply

@[simp]
theorem symm_trans_apply (eâ‚ : A â‰ƒâ‹†â‚[R] B) (eâ‚‚ : B â‰ƒâ‹†â‚[R] C) (x : C) :
    (eâ‚.trans eâ‚‚).symm x = eâ‚.symm (eâ‚‚.symm x) :=
  rfl
#align star_alg_equiv.symm_trans_apply StarAlgEquiv.symm_trans_apply

@[simp]
theorem coe_trans (eâ‚ : A â‰ƒâ‹†â‚[R] B) (eâ‚‚ : B â‰ƒâ‹†â‚[R] C) : â‡‘(eâ‚.trans eâ‚‚) = eâ‚‚ âˆ˜ eâ‚ :=
  rfl
#align star_alg_equiv.coe_trans StarAlgEquiv.coe_trans

@[simp]
theorem trans_apply (eâ‚ : A â‰ƒâ‹†â‚[R] B) (eâ‚‚ : B â‰ƒâ‹†â‚[R] C) (x : A) : (eâ‚.trans eâ‚‚) x = eâ‚‚ (eâ‚ x) :=
  rfl
#align star_alg_equiv.trans_apply StarAlgEquiv.trans_apply

theorem leftInverse_symm (e : A â‰ƒâ‹†â‚[R] B) : Function.LeftInverse e.symm e :=
  e.left_inv
#align star_alg_equiv.left_inverse_symm StarAlgEquiv.leftInverse_symm

theorem rightInverse_symm (e : A â‰ƒâ‹†â‚[R] B) : Function.RightInverse e.symm e :=
  e.right_inv
#align star_alg_equiv.right_inverse_symm StarAlgEquiv.rightInverse_symm

end Basic

section Bijective

variable {F G R A B : Type*} [Monoid R]

variable [NonUnitalNonAssocSemiring A] [DistribMulAction R A] [Star A]

variable [NonUnitalNonAssocSemiring B] [DistribMulAction R B] [Star B]

variable [hF : NonUnitalStarAlgHomClass F R A B] [NonUnitalStarAlgHomClass G R B A]

/-- If a (unital or non-unital) star algebra morphism has an inverse, it is an isomorphism of
star algebras. -/
@[simps]
def ofStarAlgHom (f : F) (g : G) (hâ‚ : âˆ€ x, g (f x) = x) (hâ‚‚ : âˆ€ x, f (g x) = x) : A â‰ƒâ‹†â‚[R] B
    where
  toFun := f
  invFun := g
  left_inv := hâ‚
  right_inv := hâ‚‚
  map_add' := map_add f
  map_mul' := map_mul f
  map_smul' := map_smul f
  map_star' := map_star f
#align star_alg_equiv.of_star_alg_hom StarAlgEquiv.ofStarAlgHom

/-- Promote a bijective star algebra homomorphism to a star algebra equivalence. -/
noncomputable def ofBijective (f : F) (hf : Function.Bijective f) : A â‰ƒâ‹†â‚[R] B :=
  {
    RingEquiv.ofBijective f
      (hf : Function.Bijective (f : A â†’ B)) with
    toFun := f
    map_star' := map_star f
    map_smul' := map_smul f }
#align star_alg_equiv.of_bijective StarAlgEquiv.ofBijective

@[simp]
theorem coe_ofBijective {f : F} (hf : Function.Bijective f) :
    (StarAlgEquiv.ofBijective f hf : A â†’ B) = f :=
  rfl
#align star_alg_equiv.coe_of_bijective StarAlgEquiv.coe_ofBijective

theorem ofBijective_apply {f : F} (hf : Function.Bijective f) (a : A) :
    (StarAlgEquiv.ofBijective f hf) a = f a :=
  rfl
#align star_alg_equiv.of_bijective_apply StarAlgEquiv.ofBijective_apply

end Bijective

end StarAlgEquiv
