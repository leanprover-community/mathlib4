/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Algebra.CharP.Invertible
import Mathlib.Data.Real.Sqrt

#align_import algebra.star.chsh from "leanprover-community/mathlib"@"31c24aa72e7b3e5ed97a8412470e904f82b81004"

/-!
# The Clauser-Horne-Shimony-Holt inequality and Tsirelson's inequality.

We establish a version of the Clauser-Horne-Shimony-Holt (CHSH) inequality
(which is a generalization of Bell's inequality).
This is a foundational result which implies that
quantum mechanics is not a local hidden variable theory.

As usually stated the CHSH inequality requires substantial language from physics and probability,
but it is possible to give a statement that is purely about ordered `*`-algebras.
We do that here, to avoid as many practical and logical dependencies as possible.
Since the algebra of observables of any quantum system is an ordered `*`-algebra
(in particular a von Neumann algebra) this is a strict generalization of the usual statement.

Let `R` be a `*`-ring.

A CHSH tuple in `R` consists of
* four elements `A‚ÇÄ A‚ÇÅ B‚ÇÄ B‚ÇÅ : R`, such that
* each `A·µ¢` and `B‚±º` is a self-adjoint involution, and
* the `A·µ¢` commute with the `B‚±º`.

The physical interpretation is that the four elements are observables (hence self-adjoint)
that take values ¬±1 (hence involutions), and that the `A·µ¢` are spacelike separated from the `B‚±º`
(and hence commute).

The CHSH inequality says that when `R` is an ordered `*`-ring
(that is, a `*`-ring which is ordered, and for every `r : R`, `0 ‚â§ star r * r`),
which is moreover *commutative*, we have
`A‚ÇÄ * B‚ÇÄ + A‚ÇÄ * B‚ÇÅ + A‚ÇÅ * B‚ÇÄ - A‚ÇÅ * B‚ÇÅ ‚â§ 2`

On the other hand, Tsirelson's inequality says that for any ordered `*`-ring we have
`A‚ÇÄ * B‚ÇÄ + A‚ÇÄ * B‚ÇÅ + A‚ÇÅ * B‚ÇÄ - A‚ÇÅ * B‚ÇÅ ‚â§ 2‚àö2`

(A caveat: in the commutative case we need 2‚Åª¬π in the ring,
and in the noncommutative case we need ‚àö2 and ‚àö2‚Åª¬π.
To keep things simple we just assume our rings are ‚Ñù-algebras.)

The proofs I've seen in the literature either
assume a significant framework for quantum mechanics,
or assume the ring is a `C^*`-algebra.
In the `C^*`-algebra case,
the order structure is completely determined by the `*`-algebra structure:
`0 ‚â§ A` iff there exists some `B` so `A = star B * B`.
There's a nice proof of both bounds in this setting at
https://en.wikipedia.org/wiki/Tsirelson%27s_bound
The proof given here is purely algebraic.

## Future work

One can show that Tsirelson's inequality is tight.
In the `*`-ring of n-by-n complex matrices, if `A ‚â§ Œª I` for some `Œª : ‚Ñù`,
then every eigenvalue has absolute value at most `Œª`.
There is a CHSH tuple in 4-by-4 matrices such that
`A‚ÇÄ * B‚ÇÄ + A‚ÇÄ * B‚ÇÅ + A‚ÇÅ * B‚ÇÄ - A‚ÇÅ * B‚ÇÅ` has `2‚àö2` as an eigenvalue.

## References

* [Clauser, Horne, Shimony, Holt,
  *Proposed experiment to test local hidden-variable theories*][zbMATH06785026]
* [Bell, *On the Einstein Podolsky Rosen Paradox*][MR3790629]
* [Tsirelson, *Quantum generalizations of Bell's inequality*][MR577178]

-/


universe u

/-- A CHSH tuple in a *-monoid consists of 4 self-adjoint involutions `A‚ÇÄ A‚ÇÅ B‚ÇÄ B‚ÇÅ` such that
the `A·µ¢` commute with the `B‚±º`.

The physical interpretation is that `A‚ÇÄ` and `A‚ÇÅ` are a pair of boolean observables which
are spacelike separated from another pair `B‚ÇÄ` and `B‚ÇÅ` of boolean observables.
-/
--@[nolint has_nonempty_instance] Porting note: linter does not exist
structure IsCHSHTuple {R} [Monoid R] [StarSemigroup R] (A‚ÇÄ A‚ÇÅ B‚ÇÄ B‚ÇÅ : R) : Prop where
  A‚ÇÄ_inv : A‚ÇÄ ^ 2 = 1
  A‚ÇÅ_inv : A‚ÇÅ ^ 2 = 1
  B‚ÇÄ_inv : B‚ÇÄ ^ 2 = 1
  B‚ÇÅ_inv : B‚ÇÅ ^ 2 = 1
  A‚ÇÄ_sa : star A‚ÇÄ = A‚ÇÄ
  A‚ÇÅ_sa : star A‚ÇÅ = A‚ÇÅ
  B‚ÇÄ_sa : star B‚ÇÄ = B‚ÇÄ
  B‚ÇÅ_sa : star B‚ÇÅ = B‚ÇÅ
  A‚ÇÄB‚ÇÄ_commutes : A‚ÇÄ * B‚ÇÄ = B‚ÇÄ * A‚ÇÄ
  A‚ÇÄB‚ÇÅ_commutes : A‚ÇÄ * B‚ÇÅ = B‚ÇÅ * A‚ÇÄ
  A‚ÇÅB‚ÇÄ_commutes : A‚ÇÅ * B‚ÇÄ = B‚ÇÄ * A‚ÇÅ
  A‚ÇÅB‚ÇÅ_commutes : A‚ÇÅ * B‚ÇÅ = B‚ÇÅ * A‚ÇÅ
set_option linter.uppercaseLean3 false in
#align is_CHSH_tuple IsCHSHTuple

variable {R : Type u}

theorem CHSH_id [CommRing R] {A‚ÇÄ A‚ÇÅ B‚ÇÄ B‚ÇÅ : R} (A‚ÇÄ_inv : A‚ÇÄ ^ 2 = 1) (A‚ÇÅ_inv : A‚ÇÅ ^ 2 = 1)
    (B‚ÇÄ_inv : B‚ÇÄ ^ 2 = 1) (B‚ÇÅ_inv : B‚ÇÅ ^ 2 = 1) :
    (2 - A‚ÇÄ * B‚ÇÄ - A‚ÇÄ * B‚ÇÅ - A‚ÇÅ * B‚ÇÄ + A‚ÇÅ * B‚ÇÅ) * (2 - A‚ÇÄ * B‚ÇÄ - A‚ÇÄ * B‚ÇÅ - A‚ÇÅ * B‚ÇÄ + A‚ÇÅ * B‚ÇÅ) =
      4 * (2 - A‚ÇÄ * B‚ÇÄ - A‚ÇÄ * B‚ÇÅ - A‚ÇÅ * B‚ÇÄ + A‚ÇÅ * B‚ÇÅ) := by
  -- If we had a Gr√∂bner basis algorithm, this would be trivial.
  -- Without one, it is somewhat tedious!
  rw [‚Üê sub_eq_zero]
  -- ‚ä¢ (2 - A‚ÇÄ * B‚ÇÄ - A‚ÇÄ * B‚ÇÅ - A‚ÇÅ * B‚ÇÄ + A‚ÇÅ * B‚ÇÅ) * (2 - A‚ÇÄ * B‚ÇÄ - A‚ÇÄ * B‚ÇÅ - A‚ÇÅ *  ‚Ä¶
  repeat'
    ring_nf
    simp only [A‚ÇÅ_inv, B‚ÇÅ_inv, sub_eq_add_neg, add_mul, mul_add, sub_mul, mul_sub, add_assoc,
      neg_add, neg_sub, sub_add, sub_sub, neg_mul, ‚Üê sq, A‚ÇÄ_inv, B‚ÇÄ_inv, ‚Üê sq, ‚Üê mul_assoc, one_mul,
      mul_one, add_right_neg, add_zero, sub_eq_add_neg, A‚ÇÄ_inv, mul_one, add_right_neg,
      zero_mul]
set_option linter.uppercaseLean3 false in
#align CHSH_id CHSH_id

/-- Given a CHSH tuple (A‚ÇÄ, A‚ÇÅ, B‚ÇÄ, B‚ÇÅ) in a *commutative* ordered `*`-algebra over ‚Ñù,
`A‚ÇÄ * B‚ÇÄ + A‚ÇÄ * B‚ÇÅ + A‚ÇÅ * B‚ÇÄ - A‚ÇÅ * B‚ÇÅ ‚â§ 2`.

(We could work over ‚Ñ§[‚Öü2] if we wanted to!)
-/
theorem CHSH_inequality_of_comm [OrderedCommRing R] [StarOrderedRing R] [Algebra ‚Ñù R]
    [OrderedSMul ‚Ñù R] (A‚ÇÄ A‚ÇÅ B‚ÇÄ B‚ÇÅ : R) (T : IsCHSHTuple A‚ÇÄ A‚ÇÅ B‚ÇÄ B‚ÇÅ) :
    A‚ÇÄ * B‚ÇÄ + A‚ÇÄ * B‚ÇÅ + A‚ÇÅ * B‚ÇÄ - A‚ÇÅ * B‚ÇÅ ‚â§ 2 := by
  let P := 2 - A‚ÇÄ * B‚ÇÄ - A‚ÇÄ * B‚ÇÅ - A‚ÇÅ * B‚ÇÄ + A‚ÇÅ * B‚ÇÅ
  -- ‚ä¢ A‚ÇÄ * B‚ÇÄ + A‚ÇÄ * B‚ÇÅ + A‚ÇÅ * B‚ÇÄ - A‚ÇÅ * B‚ÇÅ ‚â§ 2
  have i‚ÇÅ : 0 ‚â§ P := by
    have idem : P * P = 4 * P := CHSH_id T.A‚ÇÄ_inv T.A‚ÇÅ_inv T.B‚ÇÄ_inv T.B‚ÇÅ_inv
    have idem' : P = (1 / 4 : ‚Ñù) ‚Ä¢ (P * P) := by
      have h : 4 * P = (4 : ‚Ñù) ‚Ä¢ P := by simp [Algebra.smul_def]
      rw [idem, h, ‚Üê mul_smul]
      norm_num
    have sa : star P = P := by
      dsimp
      simp only [star_add, star_sub, star_mul, star_ofNat, star_one, T.A‚ÇÄ_sa, T.A‚ÇÅ_sa, T.B‚ÇÄ_sa,
        T.B‚ÇÅ_sa, mul_comm B‚ÇÄ, mul_comm B‚ÇÅ]
    rw [idem']
    conv_rhs =>
      arg 2
      arg 1
      rw [‚Üê sa]
    convert smul_le_smul_of_nonneg (R := ‚Ñù) (star_mul_self_nonneg P) _
    ¬∑ simp
    ¬∑ norm_num
  apply le_of_sub_nonneg
  -- ‚ä¢ 0 ‚â§ 2 - (A‚ÇÄ * B‚ÇÄ + A‚ÇÄ * B‚ÇÅ + A‚ÇÅ * B‚ÇÄ - A‚ÇÅ * B‚ÇÅ)
  simpa only [sub_add_eq_sub_sub, ‚Üê sub_add] using i‚ÇÅ
  -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align CHSH_inequality_of_comm CHSH_inequality_of_comm

/-!
We now prove some rather specialized lemmas in preparation for the Tsirelson inequality,
which we hide in a namespace as they are unlikely to be useful elsewhere.
-/


-- mathport name: ¬´expr‚àö2¬ª
local notation "‚àö2" => (Real.sqrt 2 : ‚Ñù)

namespace TsirelsonInequality

/-!
Before proving Tsirelson's bound,
we prepare some easy lemmas about ‚àö2.
-/


-- This calculation, which we need for Tsirelson's bound,
-- defeated me. Thanks for the rescue from Shing Tak Lam!
theorem tsirelson_inequality_aux : ‚àö2 * ‚àö2 ^ 3 = ‚àö2 * (2 * ‚àö2‚Åª¬π + 4 * (‚àö2‚Åª¬π * 2‚Åª¬π)) := by
  ring_nf
  -- ‚ä¢ Real.sqrt 2 ^ 4 = Real.sqrt 2 * (Real.sqrt 2)‚Åª¬π * 4
  rw [mul_inv_cancel (ne_of_gt (Real.sqrt_pos.2 (show (2 : ‚Ñù) > 0 by norm_num)))]
  -- ‚ä¢ Real.sqrt 2 ^ 4 = 1 * 4
  convert congr_arg (¬∑ ^ 2) (@Real.sq_sqrt 2 (by norm_num)) using 1 <;>
  -- ‚ä¢ Real.sqrt 2 ^ 4 = (Real.sqrt 2 ^ 2) ^ 2
    (try simp only [‚Üê pow_mul]) <;> norm_num
     -- ‚ä¢ Real.sqrt 2 ^ 4 = Real.sqrt 2 ^ (2 * 2)
     -- ‚ä¢ 1 * 4 = 2 ^ 2
                                    -- üéâ no goals
                                    -- üéâ no goals
#align tsirelson_inequality.tsirelson_inequality_aux TsirelsonInequality.tsirelson_inequality_aux

theorem sqrt_two_inv_mul_self : ‚àö2‚Åª¬π * ‚àö2‚Åª¬π = (2‚Åª¬π : ‚Ñù) := by
  rw [‚Üê mul_inv]
  -- ‚ä¢ (Real.sqrt 2 * Real.sqrt 2)‚Åª¬π = 2‚Åª¬π
  norm_num
  -- üéâ no goals
#align tsirelson_inequality.sqrt_two_inv_mul_self TsirelsonInequality.sqrt_two_inv_mul_self

end TsirelsonInequality

open TsirelsonInequality

/-- In a noncommutative ordered `*`-algebra over ‚Ñù,
Tsirelson's bound for a CHSH tuple (A‚ÇÄ, A‚ÇÅ, B‚ÇÄ, B‚ÇÅ) is
`A‚ÇÄ * B‚ÇÄ + A‚ÇÄ * B‚ÇÅ + A‚ÇÅ * B‚ÇÄ - A‚ÇÅ * B‚ÇÅ ‚â§ 2^(3/2) ‚Ä¢ 1`.

We prove this by providing an explicit sum-of-squares decomposition
of the difference.

(We could work over `‚Ñ§[2^(1/2), 2^(-1/2)]` if we really wanted to!)
-/
theorem tsirelson_inequality [OrderedRing R] [StarOrderedRing R] [Algebra ‚Ñù R] [OrderedSMul ‚Ñù R]
    [StarModule ‚Ñù R] (A‚ÇÄ A‚ÇÅ B‚ÇÄ B‚ÇÅ : R) (T : IsCHSHTuple A‚ÇÄ A‚ÇÅ B‚ÇÄ B‚ÇÅ) :
    A‚ÇÄ * B‚ÇÄ + A‚ÇÄ * B‚ÇÅ + A‚ÇÅ * B‚ÇÄ - A‚ÇÅ * B‚ÇÅ ‚â§ ‚àö2 ^ 3 ‚Ä¢ (1 : R) := by
  -- abel will create `‚Ñ§` multiplication. We will `simp` them away to `‚Ñù` multiplication.
  have M : ‚àÄ (m : ‚Ñ§) (a : ‚Ñù) (x : R), m ‚Ä¢ a ‚Ä¢ x = ((m : ‚Ñù) * a) ‚Ä¢ x := fun m a x => by
    rw [zsmul_eq_smul_cast ‚Ñù, ‚Üê mul_smul]
  let P := ‚àö2‚Åª¬π ‚Ä¢ (A‚ÇÅ + A‚ÇÄ) - B‚ÇÄ
  -- ‚ä¢ A‚ÇÄ * B‚ÇÄ + A‚ÇÄ * B‚ÇÅ + A‚ÇÅ * B‚ÇÄ - A‚ÇÅ * B‚ÇÅ ‚â§ Real.sqrt 2 ^ 3 ‚Ä¢ 1
  let Q := ‚àö2‚Åª¬π ‚Ä¢ (A‚ÇÅ - A‚ÇÄ) + B‚ÇÅ
  -- ‚ä¢ A‚ÇÄ * B‚ÇÄ + A‚ÇÄ * B‚ÇÅ + A‚ÇÅ * B‚ÇÄ - A‚ÇÅ * B‚ÇÅ ‚â§ Real.sqrt 2 ^ 3 ‚Ä¢ 1
  have w : ‚àö2 ^ 3 ‚Ä¢ (1 : R) - A‚ÇÄ * B‚ÇÄ - A‚ÇÄ * B‚ÇÅ - A‚ÇÅ * B‚ÇÄ + A‚ÇÅ * B‚ÇÅ = ‚àö2‚Åª¬π ‚Ä¢ (P ^ 2 + Q ^ 2) := by
    dsimp
    -- distribute out all the powers and products appearing on the RHS
    simp only [sq, sub_mul, mul_sub, add_mul, mul_add, smul_add, smul_sub]
    -- pull all coefficients out to the front, and combine `‚àö2`s where possible
    simp only [Algebra.mul_smul_comm, Algebra.smul_mul_assoc, ‚Üê mul_smul, sqrt_two_inv_mul_self]
    -- replace A·µ¢ * A·µ¢ = 1 and B·µ¢ * B·µ¢ = 1
    simp only [‚Üê sq, T.A‚ÇÄ_inv, T.A‚ÇÅ_inv, T.B‚ÇÄ_inv, T.B‚ÇÅ_inv]
    -- move A·µ¢ to the left of B·µ¢
    simp only [‚Üê T.A‚ÇÄB‚ÇÄ_commutes, ‚Üê T.A‚ÇÄB‚ÇÅ_commutes, ‚Üê T.A‚ÇÅB‚ÇÄ_commutes, ‚Üê T.A‚ÇÅB‚ÇÅ_commutes]
    -- collect terms, simplify coefficients, and collect terms again:
    abel_nf
    -- all terms coincide, but the last one. Simplify all other terms
    simp only [M]
    simp only [neg_mul, one_mul, mul_inv_cancel_of_invertible, Int.cast_one, add_assoc, add_comm,
      add_left_comm, one_smul, Int.cast_neg, neg_smul, Int.int_cast_ofNat]
    simp only [‚Üê add_assoc, ‚Üê add_smul]
    -- just look at the coefficients now:
    congr
    exact mul_left_cancel‚ÇÄ (by norm_num) tsirelson_inequality_aux
  have pos : 0 ‚â§ ‚àö2‚Åª¬π ‚Ä¢ (P ^ 2 + Q ^ 2) := by
    have P_sa : star P = P := by
      simp only [star_smul, star_add, star_sub, star_id_of_comm, T.A‚ÇÄ_sa, T.A‚ÇÅ_sa, T.B‚ÇÄ_sa, T.B‚ÇÅ_sa]
    have Q_sa : star Q = Q := by
      simp only [star_smul, star_add, star_sub, star_id_of_comm, T.A‚ÇÄ_sa, T.A‚ÇÅ_sa, T.B‚ÇÄ_sa, T.B‚ÇÅ_sa]
    have P2_nonneg : 0 ‚â§ P ^ 2 := by
      rw [sq]
      conv =>
        congr
        skip
        congr
        rw [‚Üê P_sa]
      convert (star_mul_self_nonneg P)
    have Q2_nonneg : 0 ‚â§ Q ^ 2 := by
      rw [sq]
      conv =>
        congr
        skip
        congr
        rw [‚Üê Q_sa]
      convert (star_mul_self_nonneg Q)
    convert smul_le_smul_of_nonneg (add_nonneg P2_nonneg Q2_nonneg)
        (le_of_lt (show 0 < ‚àö2‚Åª¬π by norm_num))
    -- `norm_num` can't directly show `0 ‚â§ ‚àö2‚Åª¬π`
    simp
  apply le_of_sub_nonneg
  -- ‚ä¢ 0 ‚â§ Real.sqrt 2 ^ 3 ‚Ä¢ 1 - (A‚ÇÄ * B‚ÇÄ + A‚ÇÄ * B‚ÇÅ + A‚ÇÅ * B‚ÇÄ - A‚ÇÅ * B‚ÇÅ)
  simpa only [sub_add_eq_sub_sub, ‚Üê sub_add, w, Nat.cast_zero] using pos
  -- üéâ no goals
#align tsirelson_inequality tsirelson_inequality
