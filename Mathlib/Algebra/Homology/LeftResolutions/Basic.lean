/-
Copyright (c) 2025 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.Additive
import Mathlib.Algebra.Homology.ShortComplex.Abelian
import Mathlib.Algebra.Homology.ShortComplex.HomologicalComplex
import Mathlib.Algebra.Homology.SingleHomology
import Mathlib.Algebra.Homology.Embedding.ExtendMap
import Mathlib.Algebra.Homology.Embedding.CochainComplex

/-!
# Left resolutions

Given a fully faithful functor `Î¹ : C â¥¤ A` to an abelian category,
we introduce a structure `Abelian.LeftResolutions Î¹` which gives
a functor `F : A â¥¤ C` and a natural epimorphism
`Ï€.app X : Î¹.obj (F.obj X) âŸ¶ X` for all `X : A`.
This is used in order to construct a resolution functor
`LeftResolutions.chainComplexFunctor : A â¥¤ ChainComplex C â„•`.

This shall be used in order to construct functorial flat resolutions.

-/

namespace CategoryTheory.Abelian

open Category Limits Preadditive ZeroObject

variable {A C : Type*} [Category C] [Category A] (Î¹ : C â¥¤ A)

/-- Given a fully faithful functor `Î¹ : C â¥¤ A`, this structure contains the data
of a functor `F : A â¥¤ C` and a functorial epimorphism
`Ï€.app X : Î¹.obj (F.obj X) âŸ¶ X` for all `X : A`. -/
structure LeftResolutions where
  /-- a functor which sends `X : A` to an object `F.obj X` with an epimorphism
    `Ï€.app X : Î¹.obj (F.obj X) âŸ¶ X` -/
  F : A â¥¤ C
  /-- the natural epimorphism -/
  Ï€ : F â‹™ Î¹ âŸ¶ ğŸ­ A
  hÏ€ (X : A) : Epi (Ï€.app X) := by infer_instance

namespace LeftResolutions

attribute [instance] hÏ€

variable {Î¹} (Î› : LeftResolutions Î¹) (X Y Z : A) (f : X âŸ¶ Y) (g : Y âŸ¶ Z)

@[reassoc (attr := simp)]
lemma Ï€_naturality : Î¹.map (Î›.F.map f) â‰« Î›.Ï€.app Y = Î›.Ï€.app X â‰« f :=
  Î›.Ï€.naturality f

variable [Î¹.Full] [Î¹.Faithful] [HasZeroMorphisms C] [Abelian A]

/-- Given `Î¹ : C â¥¤ A`, `Î› : LeftResolutions Î¹`, `X : A`, this is a chain complex
which is a (functorial) resolution of `A` that is obtained inductively by using
the epimorphisms given by `Î›`. -/
noncomputable def chainComplex : ChainComplex C â„• :=
  ChainComplex.mk' _ _ (Î¹.preimage (Î›.Ï€.app (kernel (Î›.Ï€.app X)) â‰« kernel.Î¹ _))
    (fun f => âŸ¨_, Î¹.preimage (Î›.Ï€.app (kernel (Î¹.map f)) â‰« kernel.Î¹ _),
      Î¹.map_injective (by simp [Î¹.map_zero])âŸ©)

/-- Given `Î› : LeftResolutions Î¹`, the chain complex `Î›.chainComplex X`
identifies in degree `0` to `Î›.F.obj X`. -/
noncomputable def chainComplexXZeroIso :
    (Î›.chainComplex X).X 0 â‰… Î›.F.obj X := Iso.refl _

/-- Given `Î› : LeftResolutions Î¹`, the chain complex `Î›.chainComplex X`
identifies in degree `1` to `Î›.F.obj (kernel (Î›.Ï€.app X))`. -/
noncomputable def chainComplexXOneIso :
    (Î›.chainComplex X).X 1 â‰… Î›.F.obj (kernel (Î›.Ï€.app X)) := Iso.refl _

@[reassoc]
lemma map_chainComplex_d_1_0 :
    Î¹.map ((Î›.chainComplex X).d 1 0) =
      Î¹.map (Î›.chainComplexXOneIso X).hom â‰« Î›.Ï€.app (kernel (Î›.Ï€.app X)) â‰« kernel.Î¹ _ â‰«
      Î¹.map (Î›.chainComplexXZeroIso X).inv := by
  simp [chainComplexXOneIso, chainComplexXZeroIso, chainComplex]

/-- The isomorphism which gives the inductive step of the construction of `Î›.chainComplex X`. -/
noncomputable def chainComplexXIso (n : â„•) :
    (Î›.chainComplex X).X (n + 2) â‰… Î›.F.obj (kernel (Î¹.map ((Î›.chainComplex X).d (n + 1) n))) := by
  apply ChainComplex.mk'XIso

lemma map_chainComplex_d (n : â„•) :
    Î¹.map ((Î›.chainComplex X).d (n + 2) (n + 1)) =
    Î¹.map (Î›.chainComplexXIso X n).hom â‰« Î›.Ï€.app (kernel (Î¹.map ((Î›.chainComplex X).d (n + 1) n))) â‰«
      kernel.Î¹ (Î¹.map ((Î›.chainComplex X).d (n + 1) n)) := by
  have := Î¹.map_preimage (Î›.Ï€.app _ â‰« kernel.Î¹ (Î¹.map ((Î›.chainComplex X).d (n + 1) n)))
  dsimp at this
  rw [â† this, â† Functor.map_comp]
  congr 1
  apply ChainComplex.mk'_d

attribute [irreducible] chainComplex

lemma exactAt_map_chainComplex_succ (n : â„•) :
    ((Î¹.mapHomologicalComplex _).obj (Î›.chainComplex X)).ExactAt (n + 1) := by
  rw [HomologicalComplex.exactAt_iff' _ (n + 2) (n + 1) n
    (ComplexShape.prev_eq' _ (by dsimp; omega)) (by simp),
    ShortComplex.exact_iff_epi_kernel_lift]
  convert epi_comp (Î¹.map (Î›.chainComplexXIso X n).hom) (Î›.Ï€.app _)
  rw [â† cancel_mono (kernel.Î¹ _), kernel.lift_Î¹]
  simp [map_chainComplex_d]

variable {X Y Z}

/-- The morphism `Î›.chainComplex X âŸ¶ Î›.chainComplex Y` of chain complexes
induced by `f : X âŸ¶ Y`. -/
noncomputable def chainComplexMap : Î›.chainComplex X âŸ¶ Î›.chainComplex Y :=
  ChainComplex.mkHom _ _
    ((Î›.chainComplexXZeroIso X).hom â‰« Î›.F.map f â‰« (Î›.chainComplexXZeroIso Y).inv)
    ((Î›.chainComplexXOneIso X).hom â‰«
      Î›.F.map (kernel.map _ _ (Î¹.map (Î›.F.map f)) f (Î›.Ï€.naturality f).symm) â‰«
      (Î›.chainComplexXOneIso Y).inv)
    (Î¹.map_injective (by
        dsimp
        simp only [Category.assoc, Functor.map_comp, map_chainComplex_d_1_0]
        simp only [â† Î¹.map_comp, â† Î¹.map_comp_assoc, Iso.inv_hom_id_assoc,
          Iso.inv_hom_id, comp_id]
        simp))
    (fun n p â†¦
      âŸ¨(Î›.chainComplexXIso X n).hom â‰« (Î›.F.map
        (kernel.map _ _ (Î¹.map p.2.1) (Î¹.map p.1) (by
          rw [â† Î¹.map_comp, â† Î¹.map_comp, p.2.2]))) â‰« (Î›.chainComplexXIso Y n).inv,
            Î¹.map_injective (by simp [map_chainComplex_d])âŸ©)

@[simp]
lemma chainComplexMap_f_0 :
    (Î›.chainComplexMap f).f 0 =
      ((Î›.chainComplexXZeroIso X).hom â‰« Î›.F.map f â‰« (Î›.chainComplexXZeroIso Y).inv) := rfl

@[simp]
lemma chainComplexMap_f_1 :
    (Î›.chainComplexMap f).f 1 =
    (Î›.chainComplexXOneIso X).hom â‰«
      Î›.F.map (kernel.map _ _ (Î¹.map (Î›.F.map f)) f (Î›.Ï€.naturality f).symm) â‰«
      (Î›.chainComplexXOneIso Y).inv := rfl

@[simp]
lemma chainComplexMap_f_succ_succ (n : â„•) :
    (Î›.chainComplexMap f).f (n + 2) =
      (Î›.chainComplexXIso X n).hom â‰«
        Î›.F.map (kernel.map _ _ (Î¹.map ((Î›.chainComplexMap f).f (n + 1)))
          (Î¹.map ((Î›.chainComplexMap f).f n))
          (by rw [â† Î¹.map_comp, â† Î¹.map_comp, HomologicalComplex.Hom.comm])) â‰«
          (Î›.chainComplexXIso Y n).inv := by
  apply ChainComplex.mkHom_f_succ_succ

variable (X) in
@[simp]
lemma chainComplexMap_id : Î›.chainComplexMap (ğŸ™ X) = ğŸ™ _ := by
  ext n
  induction n with
  | zero => simp
  | succ n hn => obtain _ | n := n <;> simp [hn]

variable (X Y) in
@[simp]
lemma chainComplexMap_zero [Î›.F.PreservesZeroMorphisms] :
    Î›.chainComplexMap (0 : X âŸ¶ Y) = 0 := by
  ext n
  induction n with
  | zero => simp
  | succ n hn => obtain _|n := n <;> simp [hn]

@[reassoc, simp]
lemma chainComplexMap_comp :
    Î›.chainComplexMap (f â‰« g) = Î›.chainComplexMap f â‰« Î›.chainComplexMap g := by
  ext n
  induction n with
  | zero => simp
  | succ n hn =>
      obtain _ | n := n
      all_goals
        simp [-Functor.map_comp, â† Î›.F.map_comp_assoc, â† Î¹.map_comp]
        congr 1
        simp [â† cancel_mono (kernel.Î¹ _), hn]

/-- Given `Î¹ : C â¥¤ A`, `Î› : LeftResolutions Î¹`, this is a
functor `A â¥¤ ChainComplex C â„•` which sends `X : A` to a resolution consisting
of objects in `C`. -/
noncomputable def chainComplexFunctor : A â¥¤ ChainComplex C â„• where
  obj X := Î›.chainComplex X
  map f := Î›.chainComplexMap f

variable [HasZeroObject C]

noncomputable def cochainComplexFunctor : A â¥¤ CochainComplex C â„¤ :=
  Î›.chainComplexFunctor â‹™ ComplexShape.embeddingDownNat.extendFunctor _

variable (X)

noncomputable abbrev cochainComplex : CochainComplex C â„¤ := Î›.cochainComplexFunctor.obj X

noncomputable def cochainComplexXZeroIso : (Î›.cochainComplex X).X 0 â‰… Î›.F.obj X :=
  (Î›.chainComplex X).extendXIso _ (by dsimp) â‰ªâ‰« Î›.chainComplexXZeroIso X

noncomputable def cochainComplexXNegOneIso :
    (Î›.cochainComplex X).X (-1) â‰… Î›.F.obj (kernel (Î›.Ï€.app X)) :=
  (Î›.chainComplex X).extendXIso _ (by dsimp) â‰ªâ‰« Î›.chainComplexXOneIso X

lemma cochainComplex_d_neg_one_zero :
    Î¹.map ((cochainComplex Î› X).d (-1) 0) = Î¹.map (cochainComplexXNegOneIso Î› X).hom â‰«
      Î›.Ï€.app (kernel (Î›.Ï€.app X)) â‰« kernel.Î¹ (Î›.Ï€.app X) â‰«
        Î¹.map (cochainComplexXZeroIso Î› X).inv := by
  dsimp [cochainComplex, cochainComplexFunctor, chainComplexFunctor,
    cochainComplexXNegOneIso]
  rw [(Î›.chainComplex X).extend_d_eq ComplexShape.embeddingDownNat (i := 1) (j := 0)
      (by simp) (by simp), Î¹.map_comp, Î¹.map_comp, map_chainComplex_d_1_0,
      Î¹.map_comp, Category.assoc, Category.assoc, Category.assoc, Category.assoc, â† Î¹.map_comp]
  rfl

noncomputable def cochainComplexÏ€ :
    (Î¹.mapHomologicalComplex _).obj (Î›.cochainComplex X) âŸ¶
      (HomologicalComplex.single A (ComplexShape.up â„¤) 0).obj X :=
  HomologicalComplex.mkHomToSingle (Î¹.map (Î›.cochainComplexXZeroIso X).hom â‰« Î›.Ï€.app X) (by
    rintro i hi
    dsimp at hi
    obtain rfl : i = -1 := by omega
    dsimp
    rw [cochainComplex_d_neg_one_zero, assoc, assoc, assoc, â† Î¹.map_comp_assoc,
      Iso.inv_hom_id, Î¹.map_id, id_comp, kernel.condition, comp_zero, comp_zero])

lemma cochainComplexÏ€_f_0 :
    (Î›.cochainComplexÏ€ X).f 0 = Î¹.map (Î›.cochainComplexXZeroIso X).hom â‰« Î›.Ï€.app X â‰«
      (HomologicalComplex.singleObjXSelf (ComplexShape.up â„¤) 0 X).inv := by
  simp [cochainComplexÏ€ ]

@[simps]
noncomputable def cochainComplexNatTransÏ€ :
    Î›.cochainComplexFunctor â‹™ Î¹.mapHomologicalComplex _ âŸ¶
      HomologicalComplex.single A (ComplexShape.up â„¤) 0 where
  app _ := Î›.cochainComplexÏ€ _
  naturality X Y f := by
    ext
    dsimp [cochainComplexFunctor, cochainComplexÏ€, cochainComplexXZeroIso, chainComplexFunctor]
    simp only [Functor.map_comp, assoc, HomologicalComplex.mkHomToSingle_f,
      Functor.mapHomologicalComplex_obj_X]
    rw [HomologicalComplex.extendMap_f _ _ (i := 0) (by dsimp)]
    dsimp
    rw [â† Î¹.map_comp_assoc, assoc, assoc, Iso.inv_hom_id, comp_id,
      HomologicalComplex.single_map_f_self, Iso.inv_hom_id_assoc]
    erw [â† Î›.Ï€.naturality_assoc f]
    dsimp
    rw [â† Î¹.map_comp_assoc, assoc, assoc, assoc, Iso.inv_hom_id, comp_id,
      Î¹.map_comp, Î¹.map_comp, assoc, assoc]

instance : (Î›.cochainComplex X).IsStrictlyLE 0 where
  isZero i hi := by
    dsimp [cochainComplex, cochainComplexFunctor]
    apply HomologicalComplex.isZero_extend_X
    intro j
    simpa using hi j

instance : CochainComplex.IsGE
    ((Î¹.mapHomologicalComplex _).obj (Î›.cochainComplex X)) 0 where
  exactAt i hi := by
    apply HomologicalComplex.ExactAt.of_iso _
      ((ComplexShape.embeddingDownNat.mapExtendFunctorNatIso Î¹).symm.app (Î›.chainComplex X))
    dsimp
    obtain âŸ¨j, hjâŸ© : âˆƒ (j : â„•), (ComplexShape.embeddingDownNat).f (j + 1) = i := by
      have : i â‰¤ -1 := by
        by_contra!
        obtain âŸ¨k, hkâŸ© := @Int.eq_ofNat_of_zero_le (a := i) (by omega)
        exact hi k (by dsimp; omega)
      obtain âŸ¨j, hjâŸ© := Int.le.dest this
      exact âŸ¨j, by dsimp; omegaâŸ©
    rw [HomologicalComplex.extend_exactAt_iff _ _ hj]
    apply exactAt_map_chainComplex_succ


open CochainComplex

instance : QuasiIsoAt (Î›.cochainComplexÏ€ X) 0 := by
  rw [quasiIsoAt_iff' _ (-1) 0 1 (by simp) (by simp),
    ShortComplex.quasiIso_iff_of_zeros' _ _ (by rfl) (by rfl)]; swap
  Â· apply (Î¹.map_isZero (isZero_of_isStrictlyLE _ 0 _ (by omega))).eq_of_tgt
  let S := ShortComplex.mk (Î›.Ï€.app (kernel (Î›.Ï€.app X)) â‰« kernel.Î¹ _) (Î›.Ï€.app X) (by simp)
  have hS : S.Exact := by
    rw [S.exact_iff_epi_kernel_lift,
      show kernel.lift S.g S.f S.zero = Î›.Ï€.app (kernel (Î›.Ï€.app X)) by
        rw [â† cancel_mono (kernel.Î¹ _), kernel.lift_Î¹]]
    infer_instance
  refine (ShortComplex.exact_and_epi_g_iff_of_iso ?_).2 âŸ¨hS, by infer_instanceâŸ©
  refine ShortComplex.isoMk (Î¹.mapIso (Î›.cochainComplexXNegOneIso X))
    (Î¹.mapIso (Î›.cochainComplexXZeroIso X))
    (HomologicalComplex.singleObjXSelf (ComplexShape.up â„¤) 0 X) ?_ ?_
  Â· dsimp
    rw [cochainComplex_d_neg_one_zero, assoc, assoc, assoc, â† Î¹.map_comp,
      Iso.inv_hom_id, Î¹.map_id]
    dsimp
    rw [comp_id]
  Â· simp [cochainComplexÏ€_f_0, S]

instance : QuasiIso (Î›.cochainComplexÏ€ X) where
  quasiIsoAt i := by
    by_cases hi : i = 0
    Â· subst hi
      infer_instance
    Â· rw [quasiIsoAt_iff_exactAt]
      Â· exact HomologicalComplex.exactAt_single_obj _ _ _ _ hi
      Â· by_cases hi' : 0 < i
        Â· exact exactAt_of_isLE _ 0 _ hi'
        Â· exact exactAt_of_isGE _ 0 _ (by omega)

instance : QuasiIso (Î›.cochainComplexNatTransÏ€.app X) := by
  dsimp
  infer_instance

end LeftResolutions

end CategoryTheory.Abelian
