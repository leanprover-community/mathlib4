/-
Copyright (c) 2025 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.LeftResolutions.Transport
import Mathlib.CategoryTheory.Idempotents.FunctorExtension
import Mathlib.CategoryTheory.MorphismProperty.Retract

/-!
# Left resolutions which preserve the zero object

The structure `LeftResolutions` allows to define a functorial
resolution of an object (see `LeftResolutions.chainComplexFunctor`
in the file `Algebra.Homology.LeftResolutions.Basic`). In
order to extend this resolution to complexes, we not only
need the functoriality but also that zero morphisms
are sent to zero. In this file, given `Î¹ : C â¥¤ A`,
we extend `Î› : LeftResolutions Î¹` to idempotent completions as
`Î›.karoubi : LeftResolutions ((functorExtensionâ‚‚ C A).obj Î¹)`, and
when both `C` and `A` are idempotent complete, we define
`Î›.reduced : LeftResolutions Î¹` in such a way that the
functor `Î›.reduced.F : A â¥¤ C` preserves zero morphisms.

For example, if `A := ModuleCat R` and `C` is the full subcategory
of flat `R`-modules, we may first define `Î›` by using the
functor which sends a `R`-module `M` to the free `R`-module
on the elements of `M`. Then, `Î›.reduced.F.obj M` will be obtained
from the free `R`-module on `M` by factoring out the direct factor
corresponding to the submodule spanned by the generator corresponding
to `0 : M` (TODO).

-/

namespace CategoryTheory

namespace NatTrans

variable {C D : Type*} [Category C] [Category D] {F G : C â¥¤ D}
  (Ï„ : F âŸ¶ G) {X Y : C} (h : Retract X Y)

-- to be moved
/-- If `X` is a retract of `Y`, then for any natural transformation `Ï„`,
the natural transformation `Ï„.app X` is a retract of `Ï„.app Y`. -/
@[simps]
def retractArrowApp : RetractArrow (Ï„.app X) (Ï„.app Y) where
  i := Arrow.homMk (F.map h.i) (G.map h.i) (by simp)
  r := Arrow.homMk (F.map h.r) (G.map h.r) (by simp)
  retract := by ext <;> simp [â† Functor.map_comp]

end NatTrans

-- to be moved
namespace Idempotents

variable {C D : Type*} [Category C] [Category D]

/-- If `X : Karoubi C`, then `X` is a retract of `((toKaroubi C).obj X.X)`. -/
@[simps]
def Karoubi.retract (X : Karoubi C) : Retract X ((toKaroubi C).obj X.X) where
  i := âŸ¨X.p, by simpâŸ©
  r := âŸ¨X.p, by simpâŸ©

/-- The precomposition of functors with `toKaroubi C` is fully faithful. -/
def whiskeringLeftObjToKaroubiFullyFaithful :
    ((Functor.whiskeringLeft C (Karoubi C) D).obj (toKaroubi C)).FullyFaithful where
  preimage {F G} Ï„ :=
    { app P := F.map P.decompId_i â‰« Ï„.app P.X â‰« G.map P.decompId_p
      naturality X Y f := by
        dsimp at Ï„ âŠ¢
        have hâ‚ : f â‰« Y.decompId_i = X.decompId_i â‰« (toKaroubi C).map f.f := by aesop
        have hâ‚‚ := Ï„.naturality f.f
        have hâ‚ƒ : X.decompId_p â‰« f = (toKaroubi C).map f.f â‰« Y.decompId_p := by aesop
        dsimp at hâ‚‚
        rw [Category.assoc, Category.assoc, â† F.map_comp_assoc,
          hâ‚, F.map_comp_assoc, reassoc_of% hâ‚‚, â† G.map_comp, â† hâ‚ƒ, G.map_comp] }
  preimage_map {F G} Ï„ := by ext X; exact (natTrans_eq _ _).symm
  map_preimage {F G} Ï„ := by
    ext X
    dsimp
    rw [Karoubi.decompId_i_toKaroubi, Karoubi.decompId_p_toKaroubi,
      Functor.map_id, Category.id_comp]
    change _ â‰« G.map (ğŸ™ _) = _
    simp

instance : (toKaroubi C).PreservesEpimorphisms where
  preserves {X Y} f _ := âŸ¨fun {Z} g h eq â†¦ by
    ext
    rw [â† cancel_epi f]
    simpa using eqâŸ©

instance : (toKaroubi C).PreservesMonomorphisms where
  preserves {X Y} f _ := âŸ¨fun {Z} g h eq â†¦ by
    ext
    rw [â† cancel_mono f]
    simpa using eqâŸ©

end Idempotents

end CategoryTheory

namespace CategoryTheory.Abelian

variable {A C : Type*} [Category C] [Category A] {Î¹ : C â¥¤ A}
  (Î› : LeftResolutions Î¹)

open Idempotents Limits MorphismProperty

namespace LeftResolutions

variable [Preadditive C] [Preadditive A] [Î¹.Additive]

/-- Auxiliary definition for `LeftResolutions.karoubi`. -/
@[simps]
def karoubi.F' : A â¥¤ Karoubi C where
  obj X := âŸ¨Î›.F.obj X, ğŸ™ _ - Î›.F.map 0, by simp [â† Functor.map_comp]âŸ©
  map {X Y} f := âŸ¨Î›.F.map f - Î›.F.map 0, by simp [â† Functor.map_comp]âŸ©
  map_comp _ _ := by simp [â† Functor.map_comp]

/-- Auxiliary definition for `LeftResolutions.karoubi`. -/
@[simps!]
def karoubi.F : Karoubi A â¥¤ Karoubi C := (functorExtensionâ‚ A C).obj (karoubi.F' Î›)

instance : (karoubi.F Î›).PreservesZeroMorphisms where

/-- Auxiliary definition for `LeftResolutions.karoubi`. -/
@[simps]
def karoubi.Ï€' : toKaroubi A â‹™ F Î› â‹™ (functorExtensionâ‚‚ C A).obj Î¹ âŸ¶ toKaroubi A where
  app X := âŸ¨Î›.Ï€.app X, by simpâŸ©

instance (X : A) : Epi ((karoubi.Ï€' Î›).app X) := by
  have h : RetractArrow ((karoubi.Ï€' Î›).app X) ((toKaroubi _).map (Î›.Ï€.app X)) :=
    { i := Arrow.homMk âŸ¨Î¹.map ((karoubi.F' Î›).obj X).p, by simp [â† Functor.map_comp]âŸ©
            (ğŸ™ _) (by simp)
      r := Arrow.homMk âŸ¨Î¹.map ((karoubi.F' Î›).obj X).p, by simp [â† Functor.map_comp]âŸ©
            (ğŸ™ _) (by simp)
      retract := by
        ext
        Â· simp [â† Functor.map_comp]
        Â· simp }
  exact of_retract (P := epimorphisms _) h (epimorphisms.infer_property _)

/-- Auxiliary definition for `LeftResolutions.karoubi`. -/
def karoubi.Ï€ : karoubi.F Î› â‹™ (functorExtensionâ‚‚ C A).obj Î¹ âŸ¶ ğŸ­ (Karoubi A) :=
  whiskeringLeftObjToKaroubiFullyFaithful.preimage (karoubi.Ï€' Î›)

@[simp]
lemma karoubi.Ï€_app_toKaroubi_obj (X : A) :
    (karoubi.Ï€ Î›).app ((toKaroubi _).obj X) = (karoubi.Ï€' Î›).app X :=
  NatTrans.congr_app (whiskeringLeftObjToKaroubiFullyFaithful.map_preimage
    (Y := ğŸ­ _) (karoubi.Ï€' Î›)) X

instance (X : A) : Epi ((karoubi.Ï€ Î›).app ((toKaroubi _).obj X)) := by
  rw [karoubi.Ï€_app_toKaroubi_obj]
  infer_instance

instance (X : Karoubi A) : Epi ((karoubi.Ï€ Î›).app X) :=
  of_retract (P := epimorphisms _) (NatTrans.retractArrowApp (karoubi.Ï€ Î›) X.retract)
    (epimorphisms.infer_property _)

/-- Given `Î¹ : C â¥¤ A`, this is the extension of `Î› : LeftResolutions Î¹` to
`LeftResolutions ((functorExtensionâ‚‚ C A).obj Î¹)`, where
`(functorExtensionâ‚‚ C A).obj Î¹ : Karoubi C â¥¤ Karoubi A` is the extension of `Î¹`. -/
@[simps]
noncomputable def karoubi : LeftResolutions ((functorExtensionâ‚‚ C A).obj Î¹) where
  F := karoubi.F Î›
  Ï€ := karoubi.Ï€ Î›

instance : Î›.karoubi.F.PreservesZeroMorphisms where

section

variable [IsIdempotentComplete A] [IsIdempotentComplete C]

/-- Given an additive functor `Î¹ : C â¥¤ A` between idempotent complete categories,
any `Î› : LeftResolutions Î¹` induces a term `Î›.reduced : LeftResolutions Î¹`
such that `Î›.reduced.F` preserves zero morphisms. -/
noncomputable def reduced : LeftResolutions Î¹ :=
  Î›.karoubi.transport (toKaroubiEquivalence A) (toKaroubiEquivalence C)
     ((karoubiUniversalâ‚ C A).unitIso.app _)

instance : Î›.reduced.F.PreservesZeroMorphisms := by
  dsimp [reduced, transport]
  infer_instance

end

end LeftResolutions

end CategoryTheory.Abelian
