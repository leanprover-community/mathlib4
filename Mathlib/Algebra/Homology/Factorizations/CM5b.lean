/-
Copyright (c) 2025 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
module

public import Mathlib.Algebra.Homology.Embedding.CochainComplex
public import Mathlib.Algebra.Homology.HomotopyCategory.MappingCone
public import Mathlib.Algebra.Homology.Factorizations.Basic

/-!
# Factorization lemma

Let `C` be an abelian category with enough injectives. We show that
any morphism `f : K âŸ¶ L` between bounded below cochain complexes in `C`
can be factored as `i â‰« p` where `i : K âŸ¶ L'` is a monomorphism (with
`L'` bounded below) and `p : L' âŸ¶ L` a quasi-isomorphism that is an epimorphism
with a degreewise injective kernel. (This is part of the factorization axiom CM5
for a model category structure on bounded below cochain complexes (TODO @joelriou).)

-/

@[expose] public section

open CategoryTheory Limits Abelian

namespace CochainComplex

variable {C : Type*} [Category* C] [Abelian C] [EnoughInjectives C]
  {K L : CochainComplex C â„¤} (f : K âŸ¶ L)

namespace cm5b

variable (K L) in
/-- Given a cochain complex `K`, this is a cochain complex `I K` with
zero differentials which in degree `n` consists of the injective
object `Injective.under (K.X n)`. -/
@[simps]
noncomputable def I : CochainComplex C â„¤ where
  X n := Injective.under (K.X n)
  d _ _ := 0

instance (n : â„¤) : Injective ((I K).X n) := by
  dsimp
  infer_instance

instance (n : â„¤) [K.IsStrictlyGE n] : (I K).IsStrictlyGE n := by
  rw [isStrictlyGE_iff]
  intro i hi
  exact Injective.isZero_under _ (K.isZero_of_isStrictlyGE n i hi)

instance (n : â„¤) [K.IsStrictlyGE (n + 1)] [L.IsStrictlyGE n] :
    (mappingCone (ğŸ™ (I K)) âŠ L).IsStrictlyGE n := by
  rw [isStrictlyGE_iff]
  intro i hi
  refine IsZero.of_iso ?_ ((HomologicalComplex.eval C (ComplexShape.up â„¤) i).mapBiprod _ _)
  simp only [HomologicalComplex.eval_obj, biprod_isZero_iff, mappingCone.isZero_X_iff, I_X]
  refine âŸ¨âŸ¨?_, ?_âŸ©, L.isZero_of_isStrictlyGE n i hiâŸ©
  all_goals exact (I K).isZero_of_isStrictlyGE (n + 1) _

variable (K L) in
/-- The second projection `mappingCone (ğŸ™ (I K)) âŠ L âŸ¶ L`. -/
noncomputable abbrev p : mappingCone (ğŸ™ (I K)) âŠ L âŸ¶ L := biprod.snd

/-- A lift of a morphism `f : K âŸ¶ L` between bounded below cochain complexes
as a monomorphism `K âŸ¶ mappingCone (ğŸ™ (I K)) âŠ L`. -/
noncomputable def i : K âŸ¶ mappingCone (ğŸ™ (I K)) âŠ L :=
  biprod.lift (mappingCone.lift _
    (HomComplex.Cocycle.mk (HomComplex.Cochain.mk (fun p q _ => K.d p q â‰« Injective.Î¹ _)) 2
      (by lia) (by
        ext p q hpq
        simp [HomComplex.Î´_v 1 2 (by lia) _ p q hpq (p + 1) (p + 1) (by lia) rfl]))
    (HomComplex.Cochain.ofHoms (fun n => Injective.Î¹ _)) (by cat_disch)) f

@[reassoc]
lemma i_f_comp (n : â„¤) : (i f).f n â‰«
    (biprod.fst : mappingCone (ğŸ™ (I K)) âŠ L âŸ¶ _).f n â‰«
      (mappingCone.snd (ğŸ™ (I K))).v n n (add_zero n) = Injective.Î¹ (K.X n) := by
  simp [i]

instance (n : â„¤) : Mono ((i f).f n) := mono_of_mono_fac (i_f_comp f n)

instance : Mono (i f) := HomologicalComplex.mono_of_mono_f (i f) inferInstance

@[reassoc (attr := simp)]
lemma fac : i f â‰« p K L = f := by simp [i]

instance (n : â„¤) : Injective ((mappingCone (ğŸ™ (I K))).X n) :=
  Injective.of_iso (HomologicalComplex.homotopyCofiber.XIsoBiprod (ğŸ™ (I K)) n (n + 1) rfl).symm
    inferInstance

variable (K L) in
lemma degreewiseEpiWithInjectiveKernel_p :
    degreewiseEpiWithInjectiveKernel (p K L) := by
  intro n
  rw [epiWithInjectiveKernel_iff]
  refine âŸ¨(mappingCone (ğŸ™ (I K))).X n, inferInstance,
    (biprod.inl :_ âŸ¶ (mappingCone (ğŸ™ (I K))) âŠ L).f n, ?_,
    (biprod.fst : (mappingCone (ğŸ™ (I K))) âŠ L âŸ¶ _).f n,
    (biprod.inr :_ âŸ¶ (mappingCone (ğŸ™ (I K))) âŠ L).f n, ?_, ?_, ?_âŸ©
  all_goals simp [â† HomologicalComplex.comp_f, â† HomologicalComplex.add_f_apply]

variable (K L) in
/-- The second projection `p K L : mappingCone (ğŸ™ (I K)) âŠ L âŸ¶ L` is a homotopy equivalence. -/
noncomputable def homotopyEquiv : HomotopyEquiv (mappingCone (ğŸ™ (I K)) âŠ L) L where
  hom := p K L
  inv := biprod.inr
  homotopyHomInvId :=
    let hâ‚€ : Homotopy (ğŸ™ (mappingCone (ğŸ™ (I K)))) 0 :=
      mappingCone.liftHomotopy _ _ _ (mappingCone.snd _) 0 (by simp) (by simp)
    let hâ‚ := (hâ‚€.compRight
      (biprod.inl : _ âŸ¶ mappingCone (ğŸ™ (I K)) âŠ L)).compLeft
        (biprod.fst : mappingCone (ğŸ™ (I K)) âŠ L âŸ¶ _)
    let hâ‚‚ := Homotopy.add hâ‚ (Homotopy.refl (biprod.snd â‰« biprod.inr))
    (Homotopy.ofEq (by simp [p])).trans (hâ‚‚.symm.trans (Homotopy.ofEq (by simp)))
  homotopyInvHomId := Homotopy.ofEq (by simp)

instance : QuasiIso (p K L) := (homotopyEquiv K L).quasiIso_hom

end cm5b

lemma cm5b (n : â„¤) [K.IsStrictlyGE (n + 1)] [L.IsStrictlyGE n] :
    âˆƒ (L' : CochainComplex C â„¤) (_hL' : L'.IsStrictlyGE n)
      (i : K âŸ¶ L') (p : L' âŸ¶ L) (_hi : Mono i)
      (_hp : degreewiseEpiWithInjectiveKernel p) (_hp' : QuasiIso p),
      i â‰« p = f :=
  âŸ¨_ , by infer_instance, cm5b.i f, cm5b.p K L, inferInstance,
    cm5b.degreewiseEpiWithInjectiveKernel_p K L, inferInstance, by simpâŸ©

end CochainComplex
