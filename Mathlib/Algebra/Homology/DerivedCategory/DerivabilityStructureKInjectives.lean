/-
Copyright (c) 2026 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.Algebra.Homology.DerivedCategory.KInjective
public import Mathlib.CategoryTheory.Abelian.Monomorphisms
public import Mathlib.CategoryTheory.Localization.DerivabilityStructure.Constructor
public import Mathlib.CategoryTheory.Localization.OfQuotient

/-!
# The K-injective derivability structure

-/

@[expose] public section

universe w

open CategoryTheory Abelian Limits ZeroObject

variable {C : Type*} [Category C] [Abelian C]

namespace CategoryTheory.Abelian

open HomologicalComplex in
lemma quasiIso_f_iff_of_shortExact {S : ShortComplex (CochainComplex C ‚Ñ§)} (hS : S.ShortExact) :
    QuasiIso S.f ‚Üî S.X‚ÇÉ.Acyclic := by
  refine ‚ü®fun _ n ‚Ü¶ ?_, fun h ‚Ü¶ ‚ü®fun n ‚Ü¶ ?_‚ü©‚ü©
  ¬∑ rw [HomologicalComplex.exactAt_iff_isZero_homology]
    refine (hS.homology_exact‚ÇÉ n (n + 1) (by simp)).isZero_X‚ÇÇ ?_ ?_
    ¬∑ rw [‚Üê cancel_epi (homologyMap S.f n), comp_zero, ‚Üê homologyMap_comp,
        S.zero, homologyMap_zero]
    ¬∑ simp [‚Üê cancel_mono (homologyMap S.f (n + 1))]
  ¬∑ rw [quasiIsoAt_iff_isIso_homologyMap]
    have := (hS.homology_exact‚ÇÇ n).epi_f (by
      apply IsZero.eq_of_tgt
      simpa [‚Üê exactAt_iff_isZero_homology] using h n)
    have := (hS.homology_exact‚ÇÅ (n - 1) n (by simp)).mono_g (by
      apply IsZero.eq_of_src
      simpa [‚Üê exactAt_iff_isZero_homology] using h (n -1))
    exact Balanced.isIso_of_mono_of_epi _

instance :
    (HomologicalComplex.quasiIso C (.up ‚Ñ§) ‚äì
      (MorphismProperty.monomorphisms _)).IsStableUnderCobaseChange where
  of_isPushout {K‚ÇÅ K‚ÇÇ L‚ÇÅ L‚ÇÇ t l b r} sq h := by
    have : Mono t := h.2
    have : Mono b := (MorphismProperty.monomorphisms _).of_isPushout sq h.2
    refine ‚ü®?_, by simpa‚ü©
    have hK : (ShortComplex.mk t (cokernel.œÄ t) (by simp)).ShortExact :=
      { exact := ShortComplex.exact_cokernel t }
    have hL : (ShortComplex.mk b (cokernel.œÄ b) (by simp)).ShortExact :=
      { exact := ShortComplex.exact_cokernel b }
    let e : cokernel t ‚âÖ cokernel b :=
      { hom := cokernel.map _ _ l r sq.w.symm
        inv := cokernel.desc _ (sq.desc 0 (cokernel.œÄ t) (by simp)) (by simp)
        hom_inv_id := coequalizer.hom_ext (by simp)
        inv_hom_id := coequalizer.hom_ext (by apply sq.hom_ext <;> simp ) }
    replace hK := quasiIso_f_iff_of_shortExact hK
    replace hL := quasiIso_f_iff_of_shortExact hL
    dsimp at hK hL
    simp only [HomologicalComplex.mem_quasiIso_iff]
    rw [hL]
    exact HomologicalComplex.Acyclic.of_iso (by simpa [‚Üê hK] using h.1) e

instance {K‚ÇÅ K‚ÇÇ L‚ÇÅ : CochainComplex C ‚Ñ§} (f : K‚ÇÅ ‚ü∂ K‚ÇÇ) (i : K‚ÇÅ ‚ü∂ L‚ÇÅ) [Mono i] [QuasiIso i] :
    QuasiIso (pushout.inl f i) :=
  (MorphismProperty.of_isPushout
    (P := (HomologicalComplex.quasiIso C (.up ‚Ñ§) ‚äì
      (MorphismProperty.monomorphisms _))) (IsPushout.of_hasPushout f i)
        ‚ü®by simpa, by simpa‚ü©).1

variable (C) in
class HasKInjectiveResolutions : Prop where
  exists_isKInjective (K : CochainComplex C ‚Ñ§) :
    ‚àÉ (L : CochainComplex C ‚Ñ§) (_ : L.IsKInjective) (i : K ‚ü∂ L), Mono i ‚àß QuasiIso i

end CategoryTheory.Abelian

namespace CochainComplex

variable (C) in
abbrev KInjectives :=
  ObjectProperty.FullSubcategory (fun (K : CochainComplex C ‚Ñ§) ‚Ü¶ K.IsKInjective)

namespace KInjectives

abbrev mk (K : CochainComplex C ‚Ñ§) [K.IsKInjective] : KInjectives C := ‚ü®K, inferInstance‚ü©

lemma mk_surjective (X : KInjectives C) :
    ‚àÉ (K : CochainComplex C ‚Ñ§) (_ : K.IsKInjective),
      X = mk K :=
  ‚ü®X.obj, X.property, rfl‚ü©

abbrev Œπ : KInjectives C ‚•§ CochainComplex C ‚Ñ§ := ObjectProperty.Œπ _

variable (C) in
def quasiIso : MorphismProperty (KInjectives C) :=
  (HomologicalComplex.quasiIso C (.up ‚Ñ§)).inverseImage Œπ

variable (C) in
@[simps]
def localizerMorphism :
    LocalizerMorphism (KInjectives.quasiIso C) (HomologicalComplex.quasiIso C (.up ‚Ñ§)) where
  functor := Œπ
  map := by rfl

variable [HasKInjectiveResolutions C]

instance (K : CochainComplex C ‚Ñ§) :
    IsConnected ((localizerMorphism C).RightResolution K) := by
  obtain ‚ü®L, _, i, _, _‚ü© := HasKInjectiveResolutions.exists_isKInjective K
  let R : (localizerMorphism C).RightResolution K := { X‚ÇÅ := .mk L, w := i, hw := by simpa }
  have : Nonempty ((localizerMorphism C).RightResolution K) := ‚ü®R‚ü©
  have : ‚àÄ R', ‚àÉ R'', Nonempty (R ‚ü∂ R'') ‚àß Nonempty (R' ‚ü∂ R'') := by
    intro R'
    obtain ‚ü®M, _, j, _, _‚ü© := HasKInjectiveResolutions.exists_isKInjective (pushout R'.w i)
    have : QuasiIso R'.w := R'.hw
    refine ‚ü®{ X‚ÇÅ := .mk M, w := R'.w ‚â´ pushout.inl R'.w i ‚â´ j, hw := ?_ }, ‚ü®?_‚ü©, ‚ü®?_‚ü©‚ü©
    ¬∑ dsimp
      simp only [HomologicalComplex.mem_quasiIso_iff]
      infer_instance
    ¬∑ exact
        { f := ObjectProperty.homMk (pushout.inr R'.w i ‚â´ j)
          comm := by simp [pushout.condition_assoc, R] }
    ¬∑ exact { f := ObjectProperty.homMk (pushout.inl R'.w i ‚â´ j) }
  refine zigzag_isConnected (fun R‚ÇÅ R‚ÇÇ ‚Ü¶ ?_)
  obtain ‚ü®R‚ÇÅ', ‚ü®f‚ÇÅ‚ü©, ‚ü®g‚ÇÅ‚ü©‚ü© := this R‚ÇÅ
  obtain ‚ü®R‚ÇÇ', ‚ü®f‚ÇÇ‚ü©, ‚ü®g‚ÇÇ‚ü©‚ü© := this R‚ÇÇ
  exact (Zigzag.of_hom g‚ÇÅ).trans ((Zigzag.of_inv f‚ÇÅ).trans
    ((Zigzag.of_hom f‚ÇÇ).trans (Zigzag.of_inv g‚ÇÇ)))

instance : (localizerMorphism C).arrow.HasRightResolutions := by
  intro f
  obtain ‚ü®K‚ÇÅ, K‚ÇÇ, f, rfl‚ü© := Arrow.mk_surjective f
  obtain ‚ü®L‚ÇÅ, _, i‚ÇÅ, _, _‚ü© := HasKInjectiveResolutions.exists_isKInjective K‚ÇÅ
  have : QuasiIso (pushout.inl f i‚ÇÅ) := inferInstance
  obtain ‚ü®M, _, j, _, _‚ü© := HasKInjectiveResolutions.exists_isKInjective (pushout f i‚ÇÅ)
  refine ‚ü®{
    X‚ÇÅ := Arrow.mk (X := .mk L‚ÇÅ) (Y := .mk M) (ObjectProperty.homMk (pushout.inr f i‚ÇÅ ‚â´ j))
    w := Arrow.homMk i‚ÇÅ (pushout.inl f i‚ÇÅ ‚â´ j) (by simp [pushout.condition_assoc])
    hw := ‚ü®by simpa, by
      dsimp
      simp only [HomologicalComplex.mem_quasiIso_iff]
      infer_instance‚ü©
  }‚ü©

end KInjectives

end CochainComplex

namespace HomotopyCategory

variable (C) in
abbrev KInjectives :=
  (HomotopyCategory.subcategoryAcyclic C).rightOrthogonal.FullSubcategory

namespace KInjectives

abbrev mk (K : CochainComplex C ‚Ñ§) [K.IsKInjective] :
    KInjectives C :=
  ‚ü®(HomotopyCategory.quotient _ _).obj K, by
    rwa [‚Üê CochainComplex.isKInjective_iff_rightOrthogonal]‚ü©

lemma mk_surjective (X : KInjectives C) :
    ‚àÉ (K : CochainComplex C ‚Ñ§) (_ : K.IsKInjective),
      X = mk K := by
  obtain ‚ü®X, hX‚ü© := X
  obtain ‚ü®K, rfl‚ü© := HomotopyCategory.quotient_obj_surjective X
  rw [‚Üê CochainComplex.isKInjective_iff_rightOrthogonal] at hX
  exact ‚ü®K, inferInstance, rfl‚ü©

def Œπ : KInjectives C ‚•§ HomotopyCategory C (.up ‚Ñ§) :=
  ObjectProperty.Œπ _

section

variable [HasDerivedCategory C]

def Qh : KInjectives C ‚•§ DerivedCategory C :=
  Œπ ‚ãô DerivedCategory.Qh

instance : (Qh (C := C)).Full where
  map_surjective {K L} f := by
    obtain ‚ü®K, _, rfl‚ü© := K.mk_surjective
    obtain ‚ü®L, _, rfl‚ü© := L.mk_surjective
    obtain ‚ü®f, rfl‚ü© := (CochainComplex.IsKInjective.Qh_map_bijective
      ((HomotopyCategory.quotient _ _).obj K) L).2 f
    exact ‚ü®ObjectProperty.homMk f, rfl‚ü©

instance : (Qh (C := C)).Faithful where
  map_injective {K L f g} h := by
    obtain ‚ü®K, _, rfl‚ü© := K.mk_surjective
    obtain ‚ü®L, _, rfl‚ü© := L.mk_surjective
    ext
    exact (CochainComplex.IsKInjective.Qh_map_bijective
      ((HomotopyCategory.quotient _ _).obj K) L).1 h

variable [HasKInjectiveResolutions C]

instance : (Qh (C := C)).EssSurj where
  mem_essImage X := by
    obtain ‚ü®K, ‚ü®e‚ü©‚ü© := Functor.EssSurj.mem_essImage (F := DerivedCategory.Qh) X
    obtain ‚ü®K, rfl‚ü© := HomotopyCategory.quotient_obj_surjective K
    obtain ‚ü®L, _, j, _, _‚ü© := HasKInjectiveResolutions.exists_isKInjective K
    have := Localization.inverts DerivedCategory.Qh (HomotopyCategory.quasiIso C (.up ‚Ñ§))
      ((HomotopyCategory.quotient _ _).map j)
        (by simpa [quotient_map_mem_quasiIso_iff])
    exact ‚ü®mk L,
      ‚ü®(asIso (DerivedCategory.Qh.map ((HomotopyCategory.quotient _ _).map j))).symm ‚â™‚â´ e‚ü©‚ü©

instance : (Qh (C := C)).IsEquivalence where

instance : Qh.IsLocalization (.isomorphisms (KInjectives C)) := by
  let l : LocalizerMorphism (.isomorphisms (KInjectives C))
      (HomotopyCategory.quasiIso C (.up ‚Ñ§)) :=
    { functor := Œπ
      map := by
        intro K L f (hf : IsIso f)
        simp only [MorphismProperty.inverseImage_iff, quasiIso]
        infer_instance }
  have : CatCommSq l.functor (ùü≠ (KInjectives C)) DerivedCategory.Qh Qh :=
    ‚ü®(Functor.leftUnitor _).symm‚ü©
  have : l.IsLocalizedEquivalence :=
    LocalizerMorphism.IsLocalizedEquivalence.mk' l (ùü≠ _) (DerivedCategory.Qh) Qh
  exact LocalizerMorphism.IsLocalizedEquivalence.isLocalization
    l DerivedCategory.Qh

end

end KInjectives

end HomotopyCategory

namespace CochainComplex

namespace KInjectives

def toHomotopyCategory : KInjectives C ‚•§ HomotopyCategory.KInjectives C :=
  ObjectProperty.lift _ (KInjectives.Œπ ‚ãô HomotopyCategory.quotient _ _) (fun K ‚Ü¶ by
    simpa [‚Üê CochainComplex.isKInjective_iff_rightOrthogonal] using K.property)

@[simp]
lemma toHomotopyCategory_obj (K : CochainComplex C ‚Ñ§) [K.IsKInjective] :
    toHomotopyCategory.obj (mk K) = HomotopyCategory.KInjectives.mk K := rfl

instance : (toHomotopyCategory (C := C)).Full where
  map_surjective {K L} f:= by
    obtain ‚ü®K, _, rfl‚ü© := K.mk_surjective
    obtain ‚ü®L, _, rfl‚ü© := L.mk_surjective
    obtain ‚ü®f, rfl‚ü© := ObjectProperty.homMk_surjective f
    obtain ‚ü®f, rfl‚ü© := (HomotopyCategory.quotient _ _).map_surjective f
    exact ‚ü®ObjectProperty.homMk f, rfl‚ü©

instance : (toHomotopyCategory (C := C)).EssSurj where
  mem_essImage X := by
    obtain ‚ü®K, _, rfl‚ü© := X.mk_surjective
    exact ‚ü®mk K, ‚ü®Iso.refl _‚ü©‚ü©

lemma isIso_toHomotopyCategory_map_iff {K L : KInjectives C} (f : K ‚ü∂ L) :
    IsIso (toHomotopyCategory.map f) ‚Üî QuasiIso f.hom := by
  have := HasDerivedCategory.standard C
  rw [‚Üê isIso_iff_of_reflects_iso _ HomotopyCategory.KInjectives.Qh]
  obtain ‚ü®K, _, rfl‚ü© := K.mk_surjective
  obtain ‚ü®L, _, rfl‚ü© := L.mk_surjective
  obtain ‚ü®f, rfl‚ü© := ObjectProperty.homMk_surjective f
  exact DerivedCategory.isIso_Q_map_iff_quasiIso C f

instance (K : CochainComplex C ‚Ñ§) [K.IsKInjective] :
    CochainComplex.IsKInjective (HomologicalComplex.cylinder K) := by
  rw [CochainComplex.isKInjective_iff_rightOrthogonal]
  refine ObjectProperty.ext_of_isTriangulatedClosed‚ÇÉ _ _
    (HomotopyCategory.mappingCone_triangleh_distinguished _) ?_ ?_
  ¬∑ dsimp
    rwa [‚Üê CochainComplex.isKInjective_iff_rightOrthogonal]
  ¬∑ dsimp
    rw [‚Üê CochainComplex.isKInjective_iff_rightOrthogonal]
    infer_instance

variable [HasKInjectiveResolutions C]

instance : toHomotopyCategory.IsLocalization (KInjectives.quasiIso C) := by
  refine Functor.isLocalization_of_essSurj_of_full_of_exists_cylinders _ _ ?_ ?_
  ¬∑ intro K L f hf
    rwa [isIso_toHomotopyCategory_map_iff]
  ¬∑ intro K L f‚ÇÄ f‚ÇÅ h
    obtain ‚ü®K, _, rfl‚ü© := K.mk_surjective
    obtain ‚ü®L, _, rfl‚ü© := L.mk_surjective
    obtain ‚ü®f‚ÇÄ, rfl‚ü© := ObjectProperty.homMk_surjective f‚ÇÄ
    obtain ‚ü®f‚ÇÅ, rfl‚ü© := ObjectProperty.homMk_surjective f‚ÇÅ
    dsimp at f‚ÇÄ f‚ÇÅ h
    let H : Homotopy f‚ÇÄ f‚ÇÅ :=
      HomotopyCategory.homotopyOfEq _ _ (HomotopyCategory.KInjectives.Œπ.congr_map h)
    refine ‚ü®mk (HomologicalComplex.cylinder K),
      ObjectProperty.homMk (HomologicalComplex.cylinder.Œπ‚ÇÄ K),
      ObjectProperty.homMk (HomologicalComplex.cylinder.Œπ‚ÇÅ K),
      ObjectProperty.homMk (HomologicalComplex.cylinder.œÄ K), ?_,
      by cat_disch, by cat_disch,
      ObjectProperty.homMk (HomologicalComplex.cylinder.desc f‚ÇÄ f‚ÇÅ H),
      by cat_disch, by cat_disch‚ü©
    ¬∑ exact (HomologicalComplex.cylinder.homotopyEquiv K
        (fun n ‚Ü¶ ‚ü®n - 1, by simp‚ü©)).quasiIso_hom

instance [HasDerivedCategory C] :
    (Œπ ‚ãô DerivedCategory.Q).IsLocalization (quasiIso C) := by
  change (toHomotopyCategory ‚ãô (HomotopyCategory.KInjectives.Qh (C := C))).IsLocalization _
  refine Functor.IsLocalization.comp toHomotopyCategory
    (HomotopyCategory.KInjectives.Qh) (W‚ÇÇ := .isomorphisms _) _ _ ?_ le_rfl ?_
  ¬∑ intro K L f hf
    have : IsIso (toHomotopyCategory.map f) := by rwa [isIso_toHomotopyCategory_map_iff]
    dsimp
    infer_instance
  ¬∑ intro K L f hf
    obtain ‚ü®K, _, rfl‚ü© := K.mk_surjective
    obtain ‚ü®L, _, rfl‚ü© := L.mk_surjective
    obtain ‚ü®f, rfl‚ü© := ObjectProperty.homMk_surjective f
    obtain ‚ü®f, rfl‚ü© := (HomotopyCategory.quotient _ _).map_surjective f
    refine ‚ü®mk K, mk L, ObjectProperty.homMk f, ?_, ‚ü®Iso.refl _‚ü©‚ü©
    have : IsIso ((HomotopyCategory.quotient _ _).map f) := by
      simp only [MorphismProperty.isomorphisms.iff] at hf
      exact (Functor.mapIso HomotopyCategory.KInjectives.Œπ (@asIso _ _ _ _ _ hf)).isIso_hom
    have : QuasiIso f := by
      rw [‚Üê DerivedCategory.isIso_Q_map_iff_quasiIso]
      exact
      (DerivedCategory.Qh.mapIso
        (asIso ((HomotopyCategory.quotient _ _).map f))).isIso_hom
    simpa [quasiIso]

instance [HasDerivedCategory C] :
    ((localizerMorphism C).functor ‚ãô DerivedCategory.Q).IsLocalization (quasiIso C) := by
  dsimp
  infer_instance

instance : (localizerMorphism C).IsLocalizedEquivalence := by
  have := HasDerivedCategory.standard C
  exact .of_isLocalization_of_isLocalization _ (DerivedCategory.Q)

instance : (localizerMorphism C).IsRightDerivabilityStructure :=
  .mk' _

end KInjectives

end CochainComplex
