/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/

import Mathlib.Algebra.Homology.DerivedCategory.Basic

/-!
# The homology sequence

In this file, we construct `homologyFunctor C n : DerivedCategory C ‚•§ C` for all `n : ‚Ñ§`,
show that they are homological functors which form a shift sequence, and construct
the long exact homology sequences associated to distinguished triangles in the
derived category.

-/

universe w v u

open CategoryTheory Pretriangulated

variable (C : Type u) [Category.{v} C] [Abelian C] [HasDerivedCategory.{w} C]

namespace DerivedCategory

/-- The homology functor `DerivedCategory C ‚•§ C` in degree `n : ‚Ñ§`. -/
noncomputable def homologyFunctor (n : ‚Ñ§) : DerivedCategory C ‚•§ C :=
  HomologicalComplexUpToQuasiIso.homologyFunctor C (ComplexShape.up ‚Ñ§) n

/-- The homology functor on the derived category is induced by the homology
functor on the category of cochain complexes. -/
noncomputable def homologyFunctorFactors (n : ‚Ñ§) : Q ‚ãô homologyFunctor C n ‚âÖ
    HomologicalComplex.homologyFunctor _ _ n :=
  HomologicalComplexUpToQuasiIso.homologyFunctorFactors C (ComplexShape.up ‚Ñ§) n

/-- The homology functor on the derived category is induced by the homology
functor on the homotopy category of cochain complexes. -/
noncomputable def homologyFunctorFactorsh (n : ‚Ñ§) : Qh ‚ãô homologyFunctor C n ‚âÖ
    HomotopyCategory.homologyFunctor _ _ n :=
  HomologicalComplexUpToQuasiIso.homologyFunctorFactorsh C (ComplexShape.up ‚Ñ§) n

noncomputable def singleFunctorCompHomologyFunctorIso (n : ‚Ñ§) :
    singleFunctor C n ‚ãô homologyFunctor C n ‚âÖ ùü≠ C :=
  isoWhiskerRight ((SingleFunctors.evaluation _ _ n).mapIso (singleFunctorsPostcompQIso C)) _ ‚â™‚â´
    Functor.associator _ _ _ ‚â™‚â´
    isoWhiskerLeft _ (homologyFunctorFactors C n) ‚â™‚â´
      HomologicalComplex.homologyFunctorSingleIso _ _ _

variable {C} in
lemma isIso_Qh_map_iff {X Y : HomotopyCategory C (ComplexShape.up ‚Ñ§)} (f : X ‚ü∂ Y) :
    IsIso (Qh.map f) ‚Üî HomotopyCategory.quasiIso C _ f := by
  constructor
  ¬∑ intro hf
    rw [HomotopyCategory.mem_quasiIso_iff]
    intro n
    rw [‚Üê NatIso.isIso_map_iff (homologyFunctorFactorsh C n) f]
    dsimp
    infer_instance
  ¬∑ exact Localization.inverts Qh (HomotopyCategory.quasiIso _ _) _

lemma isIso_iff {K L : DerivedCategory C} (f : K ‚ü∂ L) :
    IsIso f ‚Üî ‚àÄ (n : ‚Ñ§), IsIso ((homologyFunctor C n).map f) := by
  constructor
  ¬∑ intro hf n
    infer_instance
  ¬∑ intro hf
    let g := (Functor.mapArrow Qh).objPreimage (Arrow.mk f)
    refine ((MorphismProperty.isomorphisms (DerivedCategory C)).arrow_iso_iff
      ((Functor.mapArrow Qh).objObjPreimageIso (Arrow.mk f))).1 ?_
    change IsIso (Qh.map g.hom)
    rw [isIso_Qh_map_iff, HomotopyCategory.mem_quasiIso_iff]
    intro n
    have e : Arrow.mk ((homologyFunctor C n).map f) ‚âÖ
        Arrow.mk ((HomotopyCategory.homologyFunctor _ _ n).map g.hom) :=
      ((homologyFunctor C n).mapArrow.mapIso
        (((Functor.mapArrow Qh).objObjPreimageIso (Arrow.mk f)).symm)) ‚â™‚â´
        ((Functor.mapArrowFunctor _ _).mapIso (homologyFunctorFactorsh C n)).app (Arrow.mk g.hom)
    exact ((MorphismProperty.isomorphisms C).arrow_iso_iff e).1 (hf n)

instance (n : ‚Ñ§) : (homologyFunctor C n).IsHomological :=
  Functor.isHomological_of_localization Qh
    (homologyFunctor C n) _ (homologyFunctorFactorsh C n)

/-- The functors `homologyFunctor C n : DerivedCategory C ‚•§ C` for all `n : ‚Ñ§` are part
of a "shift sequence", i.e. they satisfy compatibilities with shifts. -/
noncomputable instance : (homologyFunctor C 0).ShiftSequence ‚Ñ§ :=
  Functor.ShiftSequence.induced (homologyFunctorFactorsh C 0) ‚Ñ§
    (homologyFunctor C) (homologyFunctorFactorsh C)

variable {C}

namespace HomologySequence

/-- The connecting homomorphism on the homology sequence attached to a distinguished
triangle in the derived category. -/
noncomputable def Œ¥ (T : Triangle (DerivedCategory C))
    (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (h : n‚ÇÄ + 1 = n‚ÇÅ) :
    (homologyFunctor C n‚ÇÄ).obj T.obj‚ÇÉ ‚ü∂ (homologyFunctor C n‚ÇÅ).obj T.obj‚ÇÅ :=
  (homologyFunctor C 0).shiftMap T.mor‚ÇÉ n‚ÇÄ n‚ÇÅ (by rw [add_comm 1, h])

variable (T : Triangle (DerivedCategory C)) (hT : T ‚àà distTriang _) (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (h : n‚ÇÄ + 1 = n‚ÇÅ)
include hT

@[reassoc (attr := simp)]
lemma comp_Œ¥ : (homologyFunctor C n‚ÇÄ).map T.mor‚ÇÇ ‚â´ Œ¥ T n‚ÇÄ n‚ÇÅ h = 0 :=
  (homologyFunctor C 0).comp_homologySequenceŒ¥ _ hT _ _ h

@[reassoc (attr := simp)]
lemma Œ¥_comp : Œ¥ T n‚ÇÄ n‚ÇÅ h ‚â´ (homologyFunctor C n‚ÇÅ).map T.mor‚ÇÅ = 0 :=
  (homologyFunctor C 0).homologySequenceŒ¥_comp _ hT _ _ h

lemma exact‚ÇÇ :
    (ShortComplex.mk ((homologyFunctor C n‚ÇÄ).map T.mor‚ÇÅ) ((homologyFunctor C n‚ÇÄ).map T.mor‚ÇÇ)
      (by simp only [‚Üê Functor.map_comp, comp_distTriang_mor_zero‚ÇÅ‚ÇÇ _ hT,
        Functor.map_zero])).Exact :=
  (homologyFunctor C 0).homologySequence_exact‚ÇÇ _ hT _

lemma exact‚ÇÉ : (ShortComplex.mk _ _ (comp_Œ¥ T hT n‚ÇÄ n‚ÇÅ h)).Exact :=
  (homologyFunctor C 0).homologySequence_exact‚ÇÉ _ hT _ _ h

lemma exact‚ÇÅ : (ShortComplex.mk _ _ (Œ¥_comp T hT n‚ÇÄ n‚ÇÅ h)).Exact :=
  (homologyFunctor C 0).homologySequence_exact‚ÇÅ _ hT _ _ h

lemma epi_homologyMap_mor‚ÇÅ_iff :
    Epi ((homologyFunctor C n‚ÇÄ).map T.mor‚ÇÅ) ‚Üî (homologyFunctor C n‚ÇÄ).map T.mor‚ÇÇ = 0 :=
  (homologyFunctor C 0).homologySequence_epi_shift_map_mor‚ÇÅ_iff _ hT _

lemma mono_homologyMap_mor‚ÇÅ_iff :
    Mono ((homologyFunctor C n‚ÇÅ).map T.mor‚ÇÅ) ‚Üî Œ¥ T n‚ÇÄ n‚ÇÅ h = 0 :=
  (homologyFunctor C 0).homologySequence_mono_shift_map_mor‚ÇÅ_iff _ hT _ _ h

lemma epi_homologyMap_mor‚ÇÇ_iff :
    Epi ((homologyFunctor C n‚ÇÄ).map T.mor‚ÇÇ) ‚Üî Œ¥ T n‚ÇÄ n‚ÇÅ h = 0 :=
  (homologyFunctor C 0).homologySequence_epi_shift_map_mor‚ÇÇ_iff _ hT _ _ h

lemma mono_homologyMap_mor‚ÇÇ_iff :
    Mono ((homologyFunctor C n‚ÇÄ).map T.mor‚ÇÇ) ‚Üî (homologyFunctor C n‚ÇÄ).map T.mor‚ÇÅ = 0 :=
  (homologyFunctor C 0).homologySequence_mono_shift_map_mor‚ÇÇ_iff _ hT n‚ÇÄ

lemma isIso_homologyMap_mor‚ÇÅ_iff :
    IsIso ((homologyFunctor C n‚ÇÅ).map T.mor‚ÇÅ) ‚Üî
      Œ¥ T n‚ÇÄ n‚ÇÅ h  = 0 ‚àß (homologyFunctor C n‚ÇÅ).map T.mor‚ÇÇ = 0 :=
  (homologyFunctor C 0).homologySequence_isIso_shift_map_mor‚ÇÅ_iff _ hT _ _ h

lemma isIso_homologyMap_mor‚ÇÇ_iff :
    IsIso ((homologyFunctor C n‚ÇÄ).map T.mor‚ÇÇ) ‚Üî
      Œ¥ T n‚ÇÄ n‚ÇÅ h  = 0 ‚àß (homologyFunctor C n‚ÇÄ).map T.mor‚ÇÅ = 0 :=
  (homologyFunctor C 0).homologySequence_isIso_shift_map_mor‚ÇÇ_iff _ hT _ _ h

end HomologySequence

end DerivedCategory
