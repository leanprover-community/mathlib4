/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Homology.DerivedCategory.Ext.Basic
import Mathlib.Algebra.Homology.DerivedCategory.Linear

/-!
# Ext-modules in linear categories

In this file, we show that if `C` is a `R`-linear abelian category,
then there is a `R`-module structure on the groups `Ext X Y n`
for `X` and `Y` in `C` and `n : ‚Ñï`.

-/

universe w' w t v u

namespace CategoryTheory

namespace Abelian


namespace Ext

section Ring

variable {R : Type t} [Ring R] {C : Type u} [Category.{v} C] [Abelian C] [Linear R C]
  [HasExt.{w} C]

variable {X Y : C} {n : ‚Ñï}

noncomputable instance : Module R (Ext X Y n) :=
  letI := HasDerivedCategory.standard C
  Equiv.module R homEquiv

lemma smul_eq_comp_mk‚ÇÄ (x : Ext X Y n) (r : R) :
    r ‚Ä¢ x = x.comp (mk‚ÇÄ (r ‚Ä¢ ùüô Y)) (add_zero _) := by
  letI := HasDerivedCategory.standard C
  ext
  apply ((Equiv.linearEquiv R homEquiv).map_smul r x).trans
  change r ‚Ä¢ homEquiv x = (x.comp (mk‚ÇÄ (r ‚Ä¢ ùüô Y)) (add_zero _)).hom
  rw [comp_hom, mk‚ÇÄ_hom, Functor.map_smul, Functor.map_id, ShiftedHom.mk‚ÇÄ_smul,
    ShiftedHom.comp_smul, ShiftedHom.comp_mk‚ÇÄ_id]

@[simp]
lemma smul_hom (x : Ext X Y n) (r : R) [HasDerivedCategory C] :
    (r ‚Ä¢ x).hom = r ‚Ä¢ x.hom := by
  simp [smul_eq_comp_mk‚ÇÄ]

@[simp]
lemma comp_smul {X Y Z : C} {a b : ‚Ñï} (Œ± : Ext X Y a) (Œ≤ : Ext Y Z b)
    {c : ‚Ñï} (h : a + b = c) (r : R) :
    Œ±.comp (r ‚Ä¢ Œ≤) h = r ‚Ä¢ Œ±.comp Œ≤ h := by
  letI := HasDerivedCategory.standard C
  aesop

@[simp]
lemma smul_comp {X Y Z : C} {a b : ‚Ñï} (Œ± : Ext X Y a) (Œ≤ : Ext Y Z b)
    {c : ‚Ñï} (h : a + b = c) (r : R) :
    (r ‚Ä¢ Œ±).comp Œ≤ h = r ‚Ä¢ Œ±.comp Œ≤ h := by
  letI := HasDerivedCategory.standard C
  aesop

open DerivedCategory in
/-- When an instance of `[HasDerivedCategory.{w'} C]` is available, this is the `R`-linear
equivalence between `Ext.{w} X Y n` and a type of morphisms in the derived category
of the `R`-linear abelian category `C`. -/
@[simps]
noncomputable def homLinearEquiv [HasDerivedCategory.{w'} C] :
    Ext X Y n ‚âÉ‚Çó[R]
      ShiftedHom ((singleFunctor C 0).obj X) ((singleFunctor C 0).obj Y) (n : ‚Ñ§) where
  __ := homAddEquiv
  map_smul' := by simp

end Ring

section CommRing

variable (R : Type t) [CommRing R] {C : Type u} [Category.{v} C] [Abelian C] [Linear R C]
  [HasExt.{w} C] (X Y Z : C)

/-- The composition of `Ext`, as a bilinear map. -/
@[simps!]
noncomputable def bilinearCompOfLinear (a b c : ‚Ñï) (h : a + b = c) :
    Ext X Y a ‚Üí‚Çó[R] Ext Y Z b ‚Üí‚Çó[R] Ext X Z c where
  toFun Œ± :=
    { toFun Œ≤ := Œ±.comp Œ≤ h
      map_add' := by simp
      map_smul' := by simp }
  map_add' := by aesop
  map_smul' := by aesop

end CommRing

end Ext

end Abelian

end CategoryTheory
