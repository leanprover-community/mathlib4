/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.DerivedCategory.FullyFaithful
import Mathlib.CategoryTheory.Localization.SmallShiftedHom

/-!
# Ext groups in abelian categories

Let `C` be an abelian category (with `C : Type u` and `Category.{v} C`).
In this file, we introduce the assumption `HasExt.{w} C` which asserts
that morphisms between single complexes in arbitrary degrees in
the derived category of `C` are `w`-small. Under this assumption,
we define `Ext.{w} X Y n : Type w` as shrunk versions of suitable
types of morphisms in the derived category. In particular, when `C` has
enough projectives or enough injectives, the property `HasExt.{v} C`
shall hold.

Note: in certain situations, `w := v` shall be the preferred
choice of universe (e.g. if `C := ModuleCat.{v} R` with `R : Type v`).
However, in the development of the API for Ext-groups, it is important
to keep a larger degree of generality for universes, as `w < v`
may happen in certain situations. Indeed, if `X : Scheme.{u}`,
then the underlying category of the Ã©tale site of `X` shall be a large
category. However, the category `Sheaf X.Etale AddCommGrpCat.{u}`
shall have good properties (because there is a small category of affine
schemes with the same category of sheaves), and even though the type of
morphisms in `Sheaf X.Etale AddCommGrpCat.{u}` shall be
in `Type (u + 1)`, these types are going to be `u`-small.
Then, for `C := Sheaf X.etale AddCommGrpCat.{u}`, we will have
`Category.{u + 1} C`, but `HasExt.{u} C` will hold
(as `C` has enough injectives). Then, the `Ext` groups between Ã©tale
sheaves over `X` shall be in `Type u`.

-/

assert_not_exists TwoSidedIdeal

universe w'' w' w v u

namespace CategoryTheory

variable (C : Type u) [Category.{v} C] [Abelian C]

open Localization Limits ZeroObject DerivedCategory Pretriangulated

/-- The property that morphisms between single complexes in arbitrary degrees are `w`-small
in the derived category. -/
abbrev HasExt : Prop :=
  âˆ€ (X Y : C), HasSmallLocalizedShiftedHom.{w} (HomologicalComplex.quasiIso C (ComplexShape.up â„¤)) â„¤
    ((CochainComplex.singleFunctor C 0).obj X) ((CochainComplex.singleFunctor C 0).obj Y)

lemma hasExt_iff [HasDerivedCategory.{w'} C] :
    HasExt.{w} C â†” âˆ€ (X Y : C) (n : â„¤) (_ : 0 â‰¤ n), Small.{w}
      ((singleFunctor C 0).obj X âŸ¶
        (((singleFunctor C 0).obj Y)âŸ¦nâŸ§)) := by
  dsimp [HasExt]
  simp only [hasSmallLocalizedShiftedHom_iff _ _ Q]
  constructor
  Â· intro h X Y n hn
    exact (small_congr ((shiftFunctorZero _ â„¤).app
      ((singleFunctor C 0).obj X)).homFromEquiv).1 (h X Y 0 n)
  Â· intro h X Y a b
    obtain hab | hab := le_or_gt a b
    Â· refine (small_congr ?_).1 (h X Y (b - a) (by simpa))
      exact (Functor.FullyFaithful.ofFullyFaithful
        (shiftFunctor _ a)).homEquiv.trans
        ((shiftFunctorAdd' _ _ _ _ (Int.sub_add_cancel b a)).symm.app _).homToEquiv
    Â· suffices Subsingleton ((Q.obj ((CochainComplex.singleFunctor C 0).obj X))âŸ¦aâŸ§ âŸ¶
          (Q.obj ((CochainComplex.singleFunctor C 0).obj Y))âŸ¦bâŸ§) from inferInstance
      constructor
      intro x y
      rw [â† cancel_mono ((Q.commShiftIso b).inv.app _),
        â† cancel_epi ((Q.commShiftIso a).hom.app _)]
      have : (((CochainComplex.singleFunctor C 0).obj X)âŸ¦aâŸ§).IsStrictlyLE (-a) :=
        CochainComplex.isStrictlyLE_shift _ 0 _ _ (by cutsat)
      have : (((CochainComplex.singleFunctor C 0).obj Y)âŸ¦bâŸ§).IsStrictlyGE (-b) :=
        CochainComplex.isStrictlyGE_shift _ 0 _ _ (by cutsat)
      apply (subsingleton_hom_of_isStrictlyLE_of_isStrictlyGE _ _ (-a) (-b) (by
        cutsat)).elim

lemma hasExt_of_hasDerivedCategory [HasDerivedCategory.{w} C] : HasExt.{w} C := by
  rw [hasExt_iff.{w}]
  infer_instance

lemma HasExt.standard : HasExt.{max u v} C := by
  letI := HasDerivedCategory.standard
  exact hasExt_of_hasDerivedCategory _

variable {C}

variable [HasExt.{w} C]

namespace Abelian

/-- A Ext-group in an abelian category `C`, defined as a `Type w` when `[HasExt.{w} C]`. -/
def Ext (X Y : C) (n : â„•) : Type w :=
  SmallShiftedHom.{w} (HomologicalComplex.quasiIso C (ComplexShape.up â„¤))
    ((CochainComplex.singleFunctor C 0).obj X)
    ((CochainComplex.singleFunctor C 0).obj Y) (n : â„¤)

namespace Ext

variable {X Y Z T : C}

/-- The composition of `Ext`. -/
noncomputable def comp {a b : â„•} (Î± : Ext X Y a) (Î² : Ext Y Z b) {c : â„•} (h : a + b = c) :
    Ext X Z c :=
  SmallShiftedHom.comp Î± Î² (by cutsat)

lemma comp_assoc {aâ‚ aâ‚‚ aâ‚ƒ aâ‚â‚‚ aâ‚‚â‚ƒ a : â„•} (Î± : Ext X Y aâ‚) (Î² : Ext Y Z aâ‚‚) (Î³ : Ext Z T aâ‚ƒ)
    (hâ‚â‚‚ : aâ‚ + aâ‚‚ = aâ‚â‚‚) (hâ‚‚â‚ƒ : aâ‚‚ + aâ‚ƒ = aâ‚‚â‚ƒ) (h : aâ‚ + aâ‚‚ + aâ‚ƒ = a) :
    (Î±.comp Î² hâ‚â‚‚).comp Î³ (show aâ‚â‚‚ + aâ‚ƒ = a by cutsat) =
      Î±.comp (Î².comp Î³ hâ‚‚â‚ƒ) (by cutsat) :=
  SmallShiftedHom.comp_assoc _ _ _ _ _ _ (by cutsat)

@[simp]
lemma comp_assoc_of_second_deg_zero
    {aâ‚ aâ‚ƒ aâ‚â‚ƒ : â„•} (Î± : Ext X Y aâ‚) (Î² : Ext Y Z 0) (Î³ : Ext Z T aâ‚ƒ)
    (hâ‚â‚ƒ : aâ‚ + aâ‚ƒ = aâ‚â‚ƒ) :
    (Î±.comp Î² (add_zero _)).comp Î³ hâ‚â‚ƒ = Î±.comp (Î².comp Î³ (zero_add _)) hâ‚â‚ƒ := by
  apply comp_assoc
  cutsat

@[simp]
lemma comp_assoc_of_third_deg_zero
    {aâ‚ aâ‚‚ aâ‚â‚‚ : â„•} (Î± : Ext X Y aâ‚) (Î² : Ext Y Z aâ‚‚) (Î³ : Ext Z T 0)
    (hâ‚â‚‚ : aâ‚ + aâ‚‚ = aâ‚â‚‚) :
    (Î±.comp Î² hâ‚â‚‚).comp Î³ (add_zero _) = Î±.comp (Î².comp Î³ (add_zero _)) hâ‚â‚‚ := by
  apply comp_assoc
  cutsat

section

variable [HasDerivedCategory.{w'} C]

/-- When an instance of `[HasDerivedCategory.{w'} C]` is available, this is the bijection
between `Ext.{w} X Y n` and a type of morphisms in the derived category. -/
noncomputable def homEquiv {n : â„•} :
    Ext.{w} X Y n â‰ƒ ShiftedHom ((singleFunctor C 0).obj X)
      ((singleFunctor C 0).obj Y) (n : â„¤) :=
  SmallShiftedHom.equiv (HomologicalComplex.quasiIso C (ComplexShape.up â„¤)) Q

/-- The morphism in the derived category which corresponds to an element in `Ext X Y a`. -/
noncomputable abbrev hom {a : â„•} (Î± : Ext X Y a) :
    ShiftedHom ((singleFunctor C 0).obj X) ((singleFunctor C 0).obj Y) (a : â„¤) :=
  homEquiv Î±

@[simp]
lemma comp_hom {a b : â„•} (Î± : Ext X Y a) (Î² : Ext Y Z b) {c : â„•} (h : a + b = c) :
    (Î±.comp Î² h).hom = Î±.hom.comp Î².hom (by cutsat) := by
  apply SmallShiftedHom.equiv_comp

@[ext]
lemma ext {n : â„•} {Î± Î² : Ext X Y n} (h : Î±.hom = Î².hom) : Î± = Î² :=
  homEquiv.injective h

end

/-- The canonical map `(X âŸ¶ Y) â†’ Ext X Y 0`. -/
noncomputable def mkâ‚€ (f : X âŸ¶ Y) : Ext X Y 0 := SmallShiftedHom.mkâ‚€ _ _ (by simp)
  ((CochainComplex.singleFunctor C 0).map f)

@[simp]
lemma mkâ‚€_hom [HasDerivedCategory.{w'} C] (f : X âŸ¶ Y) :
    (mkâ‚€ f).hom = ShiftedHom.mkâ‚€ _ (by simp) ((singleFunctor C 0).map f) := by
  apply SmallShiftedHom.equiv_mkâ‚€

@[simp]
lemma mkâ‚€_comp_mkâ‚€ (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    (mkâ‚€ f).comp (mkâ‚€ g) (zero_add 0) = mkâ‚€ (f â‰« g) := by
  letI := HasDerivedCategory.standard C; ext; simp

@[simp]
lemma mkâ‚€_comp_mkâ‚€_assoc (f : X âŸ¶ Y) (g : Y âŸ¶ Z) {n : â„•} (Î± : Ext Z T n) :
    (mkâ‚€ f).comp ((mkâ‚€ g).comp Î± (zero_add n)) (zero_add n) =
      (mkâ‚€ (f â‰« g)).comp Î± (zero_add n) := by
  rw [â† mkâ‚€_comp_mkâ‚€, comp_assoc]
  cutsat


variable (X Y) in
lemma mkâ‚€_bijective : Function.Bijective (mkâ‚€ (X := X) (Y := Y)) := by
  letI := HasDerivedCategory.standard C
  have h : (singleFunctor C 0).FullyFaithful := Functor.FullyFaithful.ofFullyFaithful _
  let e : (X âŸ¶ Y) â‰ƒ Ext X Y 0 :=
    (h.homEquiv.trans (ShiftedHom.homEquiv _ (by simp))).trans homEquiv.symm
  have he : e.toFun = mkâ‚€ := by
    ext f : 1
    dsimp [e]
    apply homEquiv.injective
    apply (Equiv.apply_symm_apply _ _).trans
    symm
    apply SmallShiftedHom.equiv_mkâ‚€
  rw [â† he]
  exact e.bijective

/-- The bijection `Ext X Y 0 â‰ƒ (X âŸ¶ Y)`. -/
@[simps! symm_apply]
noncomputable def homEquivâ‚€ : Ext X Y 0 â‰ƒ (X âŸ¶ Y) :=
  (Equiv.ofBijective _ (mkâ‚€_bijective X Y)).symm

@[simp]
lemma mkâ‚€_homEquivâ‚€_apply (f : Ext X Y 0) :
    mkâ‚€ (homEquivâ‚€ f) = f :=
  homEquivâ‚€.left_inv f

variable {n : â„•}

/-! The abelian group structure on `Ext X Y n` is defined by transporting the
abelian group structure on the constructed derived category
(given by `HasDerivedCategory.standard`). This constructed derived category
is used in order to obtain most of the compatibilities satisfied by
this abelian group structure. It is then shown that the bijection
`homEquiv` between `Ext X Y n` and Hom-types in the derived category
can be promoted to an additive equivalence for any `[HasDerivedCategory C]` instance. -/

noncomputable instance : AddCommGroup (Ext X Y n) :=
  letI := HasDerivedCategory.standard C
  homEquiv.addCommGroup

/-- The map from `Ext X Y n` to a `ShiftedHom` type in the *constructed* derived
category given by `HasDerivedCategory.standard`: this definition is introduced
only in order to prove properties of the abelian group structure on `Ext`-groups.
Do not use this definition: use the more general `hom` instead. -/
noncomputable abbrev hom' (Î± : Ext X Y n) :
    letI := HasDerivedCategory.standard C
    ShiftedHom ((singleFunctor C 0).obj X) ((singleFunctor C 0).obj Y) (n : â„¤) :=
  letI := HasDerivedCategory.standard C
  Î±.hom

private lemma add_hom' (Î± Î² : Ext X Y n) : (Î± + Î²).hom' = Î±.hom' + Î².hom' :=
  letI := HasDerivedCategory.standard C
  homEquiv.symm.injective (Equiv.symm_apply_apply _ _)

private lemma neg_hom' (Î± : Ext X Y n) : (-Î±).hom' = -Î±.hom' :=
  letI := HasDerivedCategory.standard C
  homEquiv.symm.injective (Equiv.symm_apply_apply _ _)

variable (X Y n) in
private lemma zero_hom' : (0 : Ext X Y n).hom' = 0 :=
  letI := HasDerivedCategory.standard C
  homEquiv.symm.injective (Equiv.symm_apply_apply _ _)

@[simp]
lemma add_comp (Î±â‚ Î±â‚‚ : Ext X Y n) {m : â„•} (Î² : Ext Y Z m) {p : â„•} (h : n + m = p) :
    (Î±â‚ + Î±â‚‚).comp Î² h = Î±â‚.comp Î² h + Î±â‚‚.comp Î² h := by
  letI := HasDerivedCategory.standard C; ext; simp [this, add_hom']

@[simp]
lemma comp_add (Î± : Ext X Y n) {m : â„•} (Î²â‚ Î²â‚‚ : Ext Y Z m) {p : â„•} (h : n + m = p) :
    Î±.comp (Î²â‚ + Î²â‚‚) h = Î±.comp Î²â‚ h + Î±.comp Î²â‚‚ h := by
  letI := HasDerivedCategory.standard C; ext; simp [this, add_hom']

@[simp]
lemma neg_comp (Î± : Ext X Y n) {m : â„•} (Î² : Ext Y Z m) {p : â„•} (h : n + m = p) :
    (-Î±).comp Î² h = -Î±.comp Î² h := by
  letI := HasDerivedCategory.standard C; ext; simp [this, neg_hom']

@[simp]
lemma comp_neg (Î± : Ext X Y n) {m : â„•} (Î² : Ext Y Z m) {p : â„•} (h : n + m = p) :
    Î±.comp (-Î²) h = -Î±.comp Î² h := by
  letI := HasDerivedCategory.standard C; ext; simp [this, neg_hom']

variable (X n) in
@[simp]
lemma zero_comp {m : â„•} (Î² : Ext Y Z m) (p : â„•) (h : n + m = p) :
    (0 : Ext X Y n).comp Î² h = 0 := by
  letI := HasDerivedCategory.standard C; ext; simp [this, zero_hom']

@[simp]
lemma comp_zero (Î± : Ext X Y n) (Z : C) (m : â„•) (p : â„•) (h : n + m = p) :
    Î±.comp (0 : Ext Y Z m) h = 0 := by
  letI := HasDerivedCategory.standard C; ext; simp [this, zero_hom']

@[simp]
lemma mkâ‚€_id_comp (Î± : Ext X Y n) :
    (mkâ‚€ (ðŸ™ X)).comp Î± (zero_add n) = Î± := by
  letI := HasDerivedCategory.standard C; ext; simp

@[simp]
lemma comp_mkâ‚€_id (Î± : Ext X Y n) :
    Î±.comp (mkâ‚€ (ðŸ™ Y)) (add_zero n) = Î± := by
  letI := HasDerivedCategory.standard C; ext; simp

variable (X Y) in
@[simp]
lemma mkâ‚€_zero : mkâ‚€ (0 : X âŸ¶ Y) = 0 := by
  letI := HasDerivedCategory.standard C; ext; simp [zero_hom']

lemma mkâ‚€_add (f g : X âŸ¶ Y) : mkâ‚€ (f + g) = mkâ‚€ f + mkâ‚€ g := by
  letI := HasDerivedCategory.standard C; ext; simp [add_hom', ShiftedHom.mkâ‚€]

/-- The additive bijection `Ext X Y 0 â‰ƒ+ (X âŸ¶ Y)`. -/
@[simps! symm_apply]
noncomputable def addEquivâ‚€ : Ext X Y 0 â‰ƒ+ (X âŸ¶ Y) where
  toEquiv := homEquivâ‚€
  map_add' x y := homEquivâ‚€.symm.injective (by simp [mkâ‚€_add])

@[simp]
lemma mkâ‚€_addEquivâ‚€_apply (f : Ext X Y 0) :
    mkâ‚€ (addEquivâ‚€ f) = f :=
  addEquivâ‚€.left_inv f

section

attribute [local instance] preservesBinaryBiproducts_of_preservesBiproducts in
lemma biprod_ext {Xâ‚ Xâ‚‚ : C} {Î± Î² : Ext (Xâ‚ âŠž Xâ‚‚) Y n}
    (hâ‚ : (mkâ‚€ biprod.inl).comp Î± (zero_add n) = (mkâ‚€ biprod.inl).comp Î² (zero_add n))
    (hâ‚‚ : (mkâ‚€ biprod.inr).comp Î± (zero_add n) = (mkâ‚€ biprod.inr).comp Î² (zero_add n)) :
    Î± = Î² := by
  letI := HasDerivedCategory.standard C
  rw [Ext.ext_iff] at hâ‚ hâ‚‚ âŠ¢
  simp only [comp_hom, mkâ‚€_hom, ShiftedHom.mkâ‚€_comp] at hâ‚ hâ‚‚
  apply BinaryCofan.IsColimit.hom_ext
    (isBinaryBilimitOfPreserves (singleFunctor C 0)
      (BinaryBiproduct.isBilimit Xâ‚ Xâ‚‚)).isColimit
  all_goals assumption

variable [HasDerivedCategory.{w'} C]

variable (X Y n) in
@[simp]
lemma zero_hom : (0 : Ext X Y n).hom = 0 := by
  let Î² : Ext 0 Y n := 0
  have hÎ² : Î².hom = 0 := by apply (Functor.map_isZero _ (isZero_zero C)).eq_of_src
  have : (0 : Ext X Y n) = (0 : Ext X 0 0).comp Î² (zero_add n) := by simp [Î²]
  rw [this, comp_hom, hÎ², ShiftedHom.comp_zero]

@[simp]
lemma add_hom (Î± Î² : Ext X Y n) : (Î± + Î²).hom = Î±.hom + Î².hom := by
  let Î±' : Ext (X âŠž X) Y n := (mkâ‚€ biprod.fst).comp Î± (zero_add n)
  let Î²' : Ext (X âŠž X) Y n := (mkâ‚€ biprod.snd).comp Î² (zero_add n)
  have eqâ‚ : Î± + Î² = (mkâ‚€ (biprod.lift (ðŸ™ X) (ðŸ™ X))).comp (Î±' + Î²') (zero_add n) := by
    simp [Î±', Î²']
  have eqâ‚‚ : Î±' + Î²' = homEquiv.symm (Î±'.hom + Î²'.hom) := by
    apply biprod_ext
    all_goals ext; simp [Î±', Î²', â† Functor.map_comp]
  simp only [eqâ‚, eqâ‚‚, comp_hom, Equiv.apply_symm_apply, ShiftedHom.comp_add]
  congr
  Â· dsimp [Î±']
    rw [comp_hom, mkâ‚€_hom, mkâ‚€_hom]
    dsimp
    rw [ShiftedHom.mkâ‚€_comp_mkâ‚€_assoc, â† Functor.map_comp,
      biprod.lift_fst, Functor.map_id, ShiftedHom.mkâ‚€_id_comp]
  Â· dsimp [Î²']
    rw [comp_hom, mkâ‚€_hom, mkâ‚€_hom]
    dsimp
    rw [ShiftedHom.mkâ‚€_comp_mkâ‚€_assoc, â† Functor.map_comp,
      biprod.lift_snd, Functor.map_id, ShiftedHom.mkâ‚€_id_comp]

lemma neg_hom (Î± : Ext X Y n) : (-Î±).hom = -Î±.hom := by
  rw [â† add_right_inj Î±.hom, â† add_hom, add_neg_cancel, add_neg_cancel, zero_hom]

/-- When an instance of `[HasDerivedCategory.{w'} C]` is available, this is the additive
bijection between `Ext.{w} X Y n` and a type of morphisms in the derived category. -/
noncomputable def homAddEquiv {n : â„•} :
    Ext.{w} X Y n â‰ƒ+
      ShiftedHom ((singleFunctor C 0).obj X) ((singleFunctor C 0).obj Y) (n : â„¤) where
  toEquiv := homEquiv
  map_add' := by simp

@[simp]
lemma homAddEquiv_apply (Î± : Ext X Y n) : homAddEquiv Î± = Î±.hom := rfl

end

variable (X Y Z) in
/-- The composition of `Ext`, as a bilinear map. -/
@[simps!]
noncomputable def bilinearComp (a b c : â„•) (h : a + b = c) :
    Ext X Y a â†’+ Ext Y Z b â†’+ Ext X Z c :=
  AddMonoidHom.mk' (fun Î± â†¦ AddMonoidHom.mk' (fun Î² â†¦ Î±.comp Î² h) (by simp)) (by aesop)

/-- The postcomposition `Ext X Y a â†’+ Ext X Z b` with `Î² : Ext Y Z n` when `a + n = b`. -/
noncomputable abbrev postcomp (Î² : Ext Y Z n) (X : C) {a b : â„•} (h : a + n = b) :
    Ext X Y a â†’+ Ext X Z b :=
  (bilinearComp X Y Z a n b h).flip Î²

/-- The precomposition `Ext Y Z a â†’+ Ext X Z b` with `Î± : Ext X Y n` when `n + a = b`. -/
noncomputable abbrev precomp (Î± : Ext X Y n) (Z : C) {a b : â„•} (h : n + a = b) :
    Ext Y Z a â†’+ Ext X Z b :=
  bilinearComp X Y Z n a b h Î±

end Ext

/-- Auxiliary definition for `extFunctor`. -/
@[simps]
noncomputable def extFunctorObj (X : C) (n : â„•) : C â¥¤ AddCommGrpCat.{w} where
  obj Y := AddCommGrpCat.of (Ext X Y n)
  map f := AddCommGrpCat.ofHom ((Ext.mkâ‚€ f).postcomp _ (add_zero n))
  map_comp f f' := by
    ext Î±
    dsimp [AddCommGrpCat.ofHom]
    rw [â† Ext.mkâ‚€_comp_mkâ‚€]
    symm
    apply Ext.comp_assoc
    omega

/-- The functor `Cáµ’áµ– â¥¤ C â¥¤ AddCommGrpCat` which sends `X : C` and `Y : C`
to `Ext X Y n`. -/
@[simps]
noncomputable def extFunctor (n : â„•) : Cáµ’áµ– â¥¤ C â¥¤ AddCommGrpCat.{w} where
  obj X := extFunctorObj X.unop n
  map {Xâ‚ Xâ‚‚} f :=
    { app := fun Y â†¦ AddCommGrpCat.ofHom (AddMonoidHom.mk'
        (fun Î± â†¦ (Ext.mkâ‚€ f.unop).comp Î± (zero_add _)) (by simp))
      naturality := fun {Yâ‚ Yâ‚‚} g â†¦ by
        ext Î±
        dsimp
        symm
        apply Ext.comp_assoc
        all_goals omega }
  map_comp {Xâ‚ Xâ‚‚ Xâ‚ƒ} f f' := by
    ext Y Î±
    simp

section biproduct

attribute [local simp] Ext.mkâ‚€_add

instance (X : C) (n : â„•) : (extFunctorObj X n).Additive where

instance (n : â„•) : (extFunctor (C := C) n).Additive where

lemma Ext.comp_sum {X Y Z : C} {p : â„•} (Î± : Ext X Y p) {Î¹ : Type*} [Fintype Î¹] {q : â„•}
    (Î² : Î¹ â†’ Ext Y Z q) {n : â„•} (h : p + q = n) :
    Î±.comp (âˆ‘ i, Î² i) h = âˆ‘ i, Î±.comp (Î² i) h :=
  map_sum (Î±.precomp Z h) _ _

lemma Ext.sum_comp {X Y Z : C} {p : â„•} {Î¹ : Type*} [Fintype Î¹] (Î± : Î¹ â†’ Ext X Y p) {q : â„•}
    (Î² : Ext Y Z q) {n : â„•} (h : p + q = n) :
    (âˆ‘ i, Î± i).comp Î² h = âˆ‘ i, (Î± i).comp Î² h :=
  map_sum (Î².postcomp X h) _ _

lemma Ext.mkâ‚€_sum {X Y : C} {Î¹ : Type*} [Fintype Î¹] (f : Î¹ â†’ (X âŸ¶ Y)) :
    mkâ‚€ (âˆ‘ i, f i) = âˆ‘ i, mkâ‚€ (f i) :=
  map_sum addEquivâ‚€.symm _ _

/-- `Ext` commutes with biproducts in its first variable. -/
noncomputable def Ext.biproductAddEquiv {J : Type*} [Fintype J] {X : J â†’ C} {c : Bicone X}
    (hc : c.IsBilimit) (Y : C) (n : â„•) : Ext c.pt Y n â‰ƒ+ Î  i, Ext (X i) Y n where
  toFun e i := (Ext.mkâ‚€ (c.Î¹ i)).comp e (zero_add n)
  invFun e := âˆ‘ (i : J), (Ext.mkâ‚€ (c.Ï€ i)).comp (e i) (zero_add n)
  left_inv x := by
    simp only [â† comp_assoc_of_second_deg_zero, mkâ‚€_comp_mkâ‚€]
    rw [â† Ext.sum_comp, â† Ext.mkâ‚€_sum, IsBilimit.total hc, mkâ‚€_id_comp]
  right_inv _ := by
    ext i
    simp only [Ext.comp_sum, â† comp_assoc_of_second_deg_zero, mkâ‚€_comp_mkâ‚€]
    rw [Finset.sum_eq_single i _ (by simp), bicone_Î¹_Ï€_self, mkâ‚€_id_comp]
    intro _ _ hij
    rw [c.Î¹_Ï€, dif_neg hij.symm, mkâ‚€_zero, zero_comp]
  map_add' _ _ := by
    simp only [comp_add, Pi.add_def]

/-- `Ext` commutes with biproducts in its second variable. -/
noncomputable def Ext.addEquivBiproduct (X : C) {J : Type*} [Fintype J] {Y : J â†’ C} {c : Bicone Y}
    (hc : c.IsBilimit) (n : â„•) : Ext X c.pt n â‰ƒ+ Î  i, Ext X (Y i) n where
  toFun e i := e.comp (Ext.mkâ‚€ (c.Ï€ i)) (add_zero n)
  invFun e := âˆ‘ (i : J), (e i).comp (Ext.mkâ‚€ (c.Î¹ i)) (add_zero n)
  left_inv _ := by
    simp only [comp_assoc_of_second_deg_zero, mkâ‚€_comp_mkâ‚€, â† Ext.comp_sum,
      â† Ext.mkâ‚€_sum, IsBilimit.total hc, comp_mkâ‚€_id]
  right_inv _ := by
    ext i
    simp only [Ext.sum_comp, comp_assoc_of_second_deg_zero, mkâ‚€_comp_mkâ‚€]
    rw [Finset.sum_eq_single i _ (by simp), bicone_Î¹_Ï€_self, comp_mkâ‚€_id]
    intro _ _ hij
    rw [c.Î¹_Ï€, dif_neg hij, mkâ‚€_zero, comp_zero]
  map_add' _ _ := by
    simp only [add_comp, Pi.add_def]

end biproduct

section ChangeOfUniverse

namespace Ext

variable [HasExt.{w'} C] {X Y : C} {n : â„•}

/-- Up to an equivalence, the type `Ext.{w} X Y n` does not depend on the universe `w`. -/
noncomputable def chgUniv : Ext.{w} X Y n â‰ƒ Ext.{w'} X Y n :=
  SmallShiftedHom.chgUniv.{w', w}

lemma homEquiv_chgUniv [HasDerivedCategory.{w''} C] (e : Ext.{w} X Y n) :
    homEquiv.{w'', w'} (chgUniv.{w'} e) = homEquiv.{w'', w} e := by
  apply SmallShiftedHom.equiv_chgUniv

end Ext

end ChangeOfUniverse

end Abelian

open Abelian

variable (C) in
lemma hasExt_iff_small_ext :
    HasExt.{w'} C â†” âˆ€ (X Y : C) (n : â„•), Small.{w'} (Ext.{w} X Y n) := by
  letI := HasDerivedCategory.standard C
  simp only [hasExt_iff, small_congr Ext.homEquiv]
  constructor
  Â· intro h X Y n
    exact h X Y n (by simp)
  Â· intro h X Y n hn
    lift n to â„• using hn
    exact h X Y n

end CategoryTheory
