/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.DerivedCategory.Basic
import Mathlib.CategoryTheory.Shift.ShiftedHom

/-!
# Ext groups in abelian categories

If `C` is an abelian category, we define types `LargeExt X Y n`
for objects `X` and `Y` in `C` and `n : â„•` as a single-field structure
containing a shifted morphism in the derived category of `C`.
Then, we need to introduce an auxiliary universe `w` and the
assumption `HasDerivedCategory.{w} C` so that `LargeExt X Y n : Type w`.
(When using the constructed localized category, we may use `w := max u v`.)

## TODO

* construct the equivalence `LargeExt X Y 0 â‰ƒ (X âŸ¶ Y)`
* construct the long exact sequences of `Ext`
* shrink the types `LargeExt X Y n` in order to define `SmallExt X Y n : Type w'`
when we know that the `Ext`-groups are `w'`-small, and redefine
`Ext := SmallExt.{v}` (which will work when `C` has enough projectives
or enough injectives).


-/

universe w v u

namespace CategoryTheory

open DerivedCategory

variable {C : Type u} [Category.{v} C] [Abelian C] [HasDerivedCategory.{w} C]

namespace Abelian

variable (X Y Z T : C) (n : â„•)

/-- The Ext-groups in an abelian category `C`, define as `Type w` when
`[HasDerivedCategory.{w} C]`. -/
@[ext]
structure LargeExt : Type w where
  /-- a shifted hom in the derived category -/
  hom : ShiftedHom ((singleFunctor C 0).obj X) ((singleFunctor C 0).obj Y) (n : â„¤)

namespace LargeExt

/-- The bijection between `LargeExt X Y n` and shifted homs in the derived category. -/
@[simps]
def equiv :
    LargeExt X Y n â‰ƒ
      ShiftedHom ((singleFunctor C 0).obj X) ((singleFunctor C 0).obj Y) (n : â„¤) where
  toFun := hom
  invFun := mk
  left_inv _ := rfl
  right_inv _ := rfl

noncomputable instance : AddCommGroup (LargeExt X Y n) := (equiv X Y n).addCommGroup

@[simp]
lemma add_hom (x y : LargeExt X Y n) : (x + y).hom = x.hom + y.hom := rfl

@[simp]
lemma sub_hom (x y : LargeExt X Y n) : (x - y).hom = x.hom - y.hom := rfl

@[simp]
lemma neg_hom (x : LargeExt X Y n) : (-x).hom = -x.hom := rfl

@[simp]
lemma zero_hom (X Y : C) (n : â„•) : (0 : LargeExt X Y n).hom = 0 := rfl

@[simp]
lemma zsmul_hom (a : â„¤) (x : LargeExt X Y n) :
    (a â€¢ x).hom = a â€¢ x.hom := rfl

variable {X Y Z T}

/-- The canonical map `(X âŸ¶ Y) â†’ LargeExt X Y 0`: -/
@[simps]
noncomputable def ofHom (f : X âŸ¶ Y) : LargeExt X Y 0 :=
  mk (ShiftedHom.mkâ‚€ ((0 : â„•) : â„¤) rfl ((singleFunctor C 0).map f))

/-- The composition on `Ext`-groups. -/
@[simps]
noncomputable def comp {a b c : â„•} (Î± : LargeExt X Y a) (Î² : LargeExt Y Z b) (h : a + b = c) :
    LargeExt X Z c where
  hom := Î±.hom.comp Î².hom (by omega)

lemma comp_assoc {aâ‚ aâ‚‚ aâ‚ƒ aâ‚â‚‚ aâ‚‚â‚ƒ a : â„•}
    (Î± : LargeExt X Y aâ‚) (Î² : LargeExt Y Z aâ‚‚) (Î³ : LargeExt Z T aâ‚ƒ)
    (hâ‚â‚‚ : aâ‚ + aâ‚‚ = aâ‚â‚‚) (hâ‚‚â‚ƒ : aâ‚‚ + aâ‚ƒ = aâ‚‚â‚ƒ) (h : aâ‚ + aâ‚‚ + aâ‚ƒ = a) :
    (Î±.comp Î² hâ‚â‚‚).comp Î³ (show _ = a by omega) =
      Î±.comp (Î².comp Î³ hâ‚‚â‚ƒ) (by omega) := by
  ext
  dsimp
  apply ShiftedHom.comp_assoc
  omega

@[simp]
lemma comp_add {a b c : â„•} (Î± : LargeExt X Y a) (Î²â‚ Î²â‚‚ : LargeExt Y Z b) (h : a + b = c) :
    Î±.comp (Î²â‚ + Î²â‚‚) h = Î±.comp Î²â‚ h + Î±.comp Î²â‚‚ h := by aesop

@[simp]
lemma add_comp {a b c : â„•} (Î±â‚ Î±â‚‚ : LargeExt X Y a) (Î² : LargeExt Y Z b) (h : a + b = c) :
    (Î±â‚ + Î±â‚‚).comp Î² h = Î±â‚.comp Î² h + Î±â‚‚.comp Î² h := by aesop

@[simp]
lemma ofHom_id_comp {a : â„•} (Î± : LargeExt X Y a) :
    (ofHom (ðŸ™ X)).comp Î± (zero_add a) = Î± := by aesop

@[simp]
lemma comp_ofHom_id {a : â„•} (Î± : LargeExt X Y a) :
    Î±.comp (ofHom (ðŸ™ Y)) (add_zero a) = Î± := by aesop

lemma ofHom_comp_ofHom (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    ofHom (f â‰« g) = (ofHom f).comp (ofHom g) (add_zero _) := by
  ext
  dsimp
  rw [Functor.map_comp]
  symm
  apply ShiftedHom.mkâ‚€_comp_mkâ‚€

/-- Auxiliary definition for `LargeExtFunctor`. -/
noncomputable def LargeExtFunctor.obj (n : â„•) (X : C) : C â¥¤ Ab where
  obj := fun Y => AddCommGroupCat.of (LargeExt X Y n)
  map := fun g => AddCommGroupCat.ofHom (AddMonoidHom.mk'
    (fun Î± â†¦ Î±.comp (ofHom g) (add_zero n)) (by aesop))
  map_comp _ _ := by
    ext
    dsimp
    simp only [ofHom_comp_ofHom]
    symm
    apply comp_assoc
    all_goals omega

variable (C)

/-- The `Ext`-groups in degree `n : â„•`, as a bifunctor `Cáµ’áµ– â¥¤ C â¥¤ Ab.{w}`. -/
noncomputable def LargeExtFunctor (n : â„•) : Cáµ’áµ– â¥¤ C â¥¤ Ab.{w} where
  obj X := LargeExtFunctor.obj n X.unop
  map {Xâ‚ Xâ‚‚} f :=
    { app := fun Y => AddCommGroupCat.ofHom (AddMonoidHom.mk'
        (fun Î² â†¦ (ofHom f.unop).comp Î² (zero_add n)) (by aesop))
      naturality := by
        intros
        ext
        symm
        dsimp [LargeExtFunctor.obj]
        apply comp_assoc
        all_goals omega }
  map_comp _ _ := by
    ext
    dsimp [LargeExtFunctor.obj]
    simp only [ofHom_comp_ofHom]
    apply comp_assoc
    all_goals omega

end LargeExt

end Abelian

end CategoryTheory
