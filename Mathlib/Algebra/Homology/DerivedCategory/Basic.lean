/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Homology.HomotopyCategory.HomologicalFunctor
import Mathlib.Algebra.Homology.HomotopyCategory.ShiftSequence
import Mathlib.Algebra.Homology.HomotopyCategory.SingleFunctors
import Mathlib.Algebra.Homology.HomotopyCategory.Triangulated
import Mathlib.Algebra.Homology.Localization

/-! # The derived category of an abelian category

In this file, we construct the derived category `DerivedCategory C` of an
abelian category `C`. It is equipped with a triangulated structure.

The derived category is defined here as the localization of cochain complexes
indexed by `‚Ñ§` with respect to quasi-isomorphisms: it is a type synonym of
`HomologicalComplexUpToQuasiIso C (ComplexShape.up ‚Ñ§)`. Then, we have a
localization functor `DerivedCategory.Q : CochainComplex C ‚Ñ§ ‚•§ DerivedCategory C`.
It was already shown in the file `Algebra.Homology.Localization` that the induced
functor `DerivedCategory.Qh : HomotopyCategory C (ComplexShape.up ‚Ñ§) ‚•§ DerivedCategory C`
is a localization functor with respect to the class of morphisms
`HomotopyCategory.quasiIso C (ComplexShape.up ‚Ñ§)`. In the lemma
`HomotopyCategory.quasiIso_eq_subcategoryAcyclic_W` we obtain that this class of morphisms
consists of morphisms whose cone belongs to the triangulated subcategory
`HomotopyCategory.subcategoryAcyclic C` of acyclic complexes. Then, the triangulated
structure on `DerivedCategory C` is deduced from the triangulated structure
on the homotopy category (see file `Algebra.Homology.HomotopyCategory.Triangulated`)
using the localization theorem for triangulated categories which was obtained
in the file `CategoryTheory.Localization.Triangulated`.

## Implementation notes

If `C : Type u` and `Category.{v} C`, the constructed localized category of cochain
complexes with respect to quasi-isomorphisms has morphisms in `Type (max u v)`.
However, in certain circumstances, it shall be possible to prove that they are `v`-small
(when `C` is a Grothendieck abelian category (e.g. the category of modules over a ring),
it should be so by a theorem of Hovey.).

Then, when working with derived categories in mathlib, the user should add the variable
`[HasDerivedCategory.{w} C]` which is the assumption that there is a chosen derived
category with morphisms in `Type w`. When derived categories are used in order to
prove statements which do not involve derived categories, the `HasDerivedCategory.{max u v}`
instance should be obtained at the beginning of the proof, using the term
`HasDerivedCategory.standard C`.

## TODO (@joelriou)

- construct the distinguished triangle associated to a short exact sequence
of cochain complexes (done), and compare the associated connecting homomorphism
with the one defined in `Algebra.Homology.HomologySequence`.

## References
* [Jean-Louis Verdier, *Des cat√©gories d√©riv√©es des cat√©gories ab√©liennes*][verdier1996]
* [Mark Hovey, *Model category structures on chain complexes of sheaves*][hovey-2001]

-/

assert_not_exists TwoSidedIdeal

universe w v u

open CategoryTheory Limits Pretriangulated

variable (C : Type u) [Category.{v} C] [Abelian C]

namespace HomotopyCategory

/-- The triangulated subcategory of `HomotopyCategory C (ComplexShape.up ‚Ñ§)` consisting
of acyclic complexes. -/
def subcategoryAcyclic : ObjectProperty (HomotopyCategory C (ComplexShape.up ‚Ñ§)) :=
  (homologyFunctor C (ComplexShape.up ‚Ñ§) 0).homologicalKernel

instance : (subcategoryAcyclic C).IsTriangulated := by
  dsimp [subcategoryAcyclic]
  infer_instance

instance : (subcategoryAcyclic C).IsClosedUnderIsomorphisms := by
  dsimp [subcategoryAcyclic]
  infer_instance

variable {C}

lemma mem_subcategoryAcyclic_iff (X : HomotopyCategory C (ComplexShape.up ‚Ñ§)) :
    subcategoryAcyclic C X ‚Üî ‚àÄ (n : ‚Ñ§), IsZero ((homologyFunctor _ _ n).obj X) :=
  Functor.mem_homologicalKernel_iff _ X

lemma quotient_obj_mem_subcategoryAcyclic_iff_exactAt (K : CochainComplex C ‚Ñ§) :
    subcategoryAcyclic C ((quotient _ _).obj K) ‚Üî ‚àÄ (n : ‚Ñ§), K.ExactAt n := by
  rw [mem_subcategoryAcyclic_iff]
  refine forall_congr' (fun n => ?_)
  simp only [HomologicalComplex.exactAt_iff_isZero_homology]
  exact ((homologyFunctorFactors C (ComplexShape.up ‚Ñ§) n).app K).isZero_iff

variable (C)

lemma quasiIso_eq_subcategoryAcyclic_W :
    quasiIso C (ComplexShape.up ‚Ñ§) = (subcategoryAcyclic C).trW := by
  ext K L f
  exact ((homologyFunctor C (ComplexShape.up ‚Ñ§) 0).mem_homologicalKernel_trW_iff f).symm

end HomotopyCategory

/-- The assumption that a localized category for
`(HomologicalComplex.quasiIso C (ComplexShape.up ‚Ñ§))` has been chosen, and that the morphisms
in this chosen category are in `Type w`. -/
abbrev HasDerivedCategory := MorphismProperty.HasLocalization.{w}
  (HomologicalComplex.quasiIso C (ComplexShape.up ‚Ñ§))

/-- The derived category obtained using the constructed localized category of cochain complexes
with respect to quasi-isomorphisms. This should be used only while proving statements
which do not involve the derived category. -/
def HasDerivedCategory.standard : HasDerivedCategory.{max u v} C :=
  MorphismProperty.HasLocalization.standard _

variable [HasDerivedCategory.{w} C]

/-- The derived category of an abelian category. -/
def DerivedCategory : Type (max u v) := HomologicalComplexUpToQuasiIso C (ComplexShape.up ‚Ñ§)

namespace DerivedCategory

instance : Category.{w} (DerivedCategory C) := by
  dsimp [DerivedCategory]
  infer_instance

variable {C}

/-- The localization functor `CochainComplex C ‚Ñ§ ‚•§ DerivedCategory C`. -/
def Q : CochainComplex C ‚Ñ§ ‚•§ DerivedCategory C := HomologicalComplexUpToQuasiIso.Q

instance : (Q (C := C)).IsLocalization
    (HomologicalComplex.quasiIso C (ComplexShape.up ‚Ñ§)) := by
  dsimp only [Q, DerivedCategory]
  infer_instance

instance {K L : CochainComplex C ‚Ñ§} (f : K ‚ü∂ L) [QuasiIso f] :
    IsIso (Q.map f) :=
  Localization.inverts Q (HomologicalComplex.quasiIso C (ComplexShape.up ‚Ñ§)) _
    (inferInstanceAs (QuasiIso f))

/-- The localization functor `HomotopyCategory C (ComplexShape.up ‚Ñ§) ‚•§ DerivedCategory C`. -/
def Qh : HomotopyCategory C (ComplexShape.up ‚Ñ§) ‚•§ DerivedCategory C :=
  HomologicalComplexUpToQuasiIso.Qh

variable (C)

/-- The natural isomorphism `HomotopyCategory.quotient C (ComplexShape.up ‚Ñ§) ‚ãô Qh ‚âÖ Q`. -/
def quotientCompQhIso : HomotopyCategory.quotient C (ComplexShape.up ‚Ñ§) ‚ãô Qh ‚âÖ Q :=
  HomologicalComplexUpToQuasiIso.quotientCompQhIso C (ComplexShape.up ‚Ñ§)

instance : Qh.IsLocalization (HomotopyCategory.quasiIso C (ComplexShape.up ‚Ñ§)) := by
  dsimp [Qh, DerivedCategory]
  infer_instance

instance : Qh.IsLocalization (HomotopyCategory.subcategoryAcyclic C).trW := by
  rw [‚Üê HomotopyCategory.quasiIso_eq_subcategoryAcyclic_W]
  infer_instance

noncomputable instance : Preadditive (DerivedCategory C) :=
  Localization.preadditive Qh (HomotopyCategory.subcategoryAcyclic C).trW

instance : (Qh (C := C)).Additive :=
  Localization.functor_additive Qh (HomotopyCategory.subcategoryAcyclic C).trW

instance : (Q (C := C)).Additive :=
  Functor.additive_of_iso (quotientCompQhIso C)

noncomputable instance : HasZeroObject (DerivedCategory C) :=
  Q.hasZeroObject_of_additive

noncomputable instance : HasShift (DerivedCategory C) ‚Ñ§ :=
  HasShift.localized Qh (HomotopyCategory.subcategoryAcyclic C).trW ‚Ñ§

noncomputable instance : (Qh (C := C)).CommShift ‚Ñ§ :=
  Functor.CommShift.localized Qh (HomotopyCategory.subcategoryAcyclic C).trW ‚Ñ§

noncomputable instance : (Q (C := C)).CommShift ‚Ñ§ :=
  Functor.CommShift.ofIso (quotientCompQhIso C) ‚Ñ§

instance : NatTrans.CommShift (quotientCompQhIso C).hom ‚Ñ§ :=
  Functor.CommShift.ofIso_compatibility (quotientCompQhIso C) ‚Ñ§

instance (n : ‚Ñ§) : (shiftFunctor (DerivedCategory C) n).Additive := by
  rw [Localization.functor_additive_iff
    Qh (HomotopyCategory.subcategoryAcyclic C).trW]
  exact Functor.additive_of_iso (Qh.commShiftIso n)

noncomputable instance : Pretriangulated (DerivedCategory C) :=
  Triangulated.Localization.pretriangulated
    Qh (HomotopyCategory.subcategoryAcyclic C).trW

instance : (Qh (C := C)).IsTriangulated :=
  Triangulated.Localization.isTriangulated_functor
    Qh (HomotopyCategory.subcategoryAcyclic C).trW

noncomputable instance : IsTriangulated (DerivedCategory C) :=
  Triangulated.Localization.isTriangulated
    Qh (HomotopyCategory.subcategoryAcyclic C).trW

instance : (Qh (C := C)).mapArrow.EssSurj :=
  Localization.essSurj_mapArrow _ (HomotopyCategory.subcategoryAcyclic C).trW

instance {D : Type*} [Category D] : ((Functor.whiskeringLeft _ _ D).obj (Qh (C := C))).Full :=
  inferInstanceAs
    (Localization.whiskeringLeftFunctor' _ (HomotopyCategory.quasiIso _ _) D).Full

instance {D : Type*} [Category D] : ((Functor.whiskeringLeft _ _ D).obj (Qh (C := C))).Faithful :=
  inferInstanceAs
    (Localization.whiskeringLeftFunctor' _ (HomotopyCategory.quasiIso _ _) D).Faithful

instance : (Qh : _ ‚•§ DerivedCategory C).EssSurj :=
  Localization.essSurj _ (HomotopyCategory.quasiIso _ _)

instance : (Q : _ ‚•§ DerivedCategory C).EssSurj :=
  Localization.essSurj _ (HomologicalComplex.quasiIso _ _)

variable {C} in
lemma mem_distTriang_iff (T : Triangle (DerivedCategory C)) :
    (T ‚àà distTriang (DerivedCategory C)) ‚Üî ‚àÉ (X Y : CochainComplex C ‚Ñ§) (f : X ‚ü∂ Y),
      Nonempty (T ‚âÖ Q.mapTriangle.obj (CochainComplex.mappingCone.triangle f)) := by
  constructor
  ¬∑ rintro ‚ü®T', e, ‚ü®X, Y, f, ‚ü®e'‚ü©‚ü©‚ü©
    refine ‚ü®_, _, f, ‚ü®?_‚ü©‚ü©
    exact e ‚â™‚â´ Qh.mapTriangle.mapIso e' ‚â™‚â´
      (Functor.mapTriangleCompIso (HomotopyCategory.quotient C _) Qh).symm.app _ ‚â™‚â´
      (Functor.mapTriangleIso (quotientCompQhIso C)).app _
  ¬∑ rintro ‚ü®X, Y, f, ‚ü®e‚ü©‚ü©
    refine isomorphic_distinguished _ (Qh.map_distinguished _ ?_) _
      (e ‚â™‚â´ (Functor.mapTriangleIso (quotientCompQhIso C)).symm.app _ ‚â™‚â´
      (Functor.mapTriangleCompIso (HomotopyCategory.quotient C _) Qh).app _)
    exact ‚ü®_, _, f, ‚ü®Iso.refl _‚ü©‚ü©

/-- The single functors `C ‚•§ DerivedCategory C` for all `n : ‚Ñ§` along with
their compatibilities with shifts. -/
noncomputable def singleFunctors : SingleFunctors C (DerivedCategory C) ‚Ñ§ :=
  (HomotopyCategory.singleFunctors C).postcomp Qh

/-- The shift functor `C ‚•§ DerivedCategory C` which sends `X : C` to the
single cochain complex with `X` sitting in degree `n : ‚Ñ§`. -/
noncomputable abbrev singleFunctor (n : ‚Ñ§) := (singleFunctors C).functor n

instance (n : ‚Ñ§) : (singleFunctor C n).Additive := by
  dsimp [singleFunctor, singleFunctors]
  infer_instance

-- The object level definitional equality underlying `singleFunctorsPostcompQhIso`.
@[simp] theorem Qh_obj_singleFunctors_obj (n : ‚Ñ§) (X : C) :
    Qh.obj (((HomotopyCategory.singleFunctors C).functor n).obj X) = (singleFunctor C n).obj X := by
  rfl

/-- The isomorphism
`DerivedCategory.singleFunctors C ‚âÖ (HomotopyCategory.singleFunctors C).postcomp Qh` given
by the definition of `DerivedCategory.singleFunctors`. -/
noncomputable def singleFunctorsPostcompQhIso :
    singleFunctors C ‚âÖ (HomotopyCategory.singleFunctors C).postcomp Qh :=
  Iso.refl _

/-- The isomorphism
`DerivedCategory.singleFunctors C ‚âÖ (CochainComplex.singleFunctors C).postcomp Q`. -/
noncomputable def singleFunctorsPostcompQIso :
    singleFunctors C ‚âÖ (CochainComplex.singleFunctors C).postcomp Q :=
  (SingleFunctors.postcompFunctor C ‚Ñ§ (Qh : _ ‚•§ DerivedCategory C)).mapIso
    (HomotopyCategory.singleFunctorsPostcompQuotientIso C) ‚â™‚â´
      (CochainComplex.singleFunctors C).postcompPostcompIso (HomotopyCategory.quotient _ _) Qh ‚â™‚â´
      SingleFunctors.postcompIsoOfIso
        (CochainComplex.singleFunctors C) (quotientCompQhIso C)

lemma singleFunctorsPostcompQIso_hom_hom (n : ‚Ñ§) :
    (singleFunctorsPostcompQIso C).hom.hom n = ùüô _ := by
  ext X
  dsimp [singleFunctorsPostcompQIso, HomotopyCategory.singleFunctorsPostcompQuotientIso,
    quotientCompQhIso, HomologicalComplexUpToQuasiIso.quotientCompQhIso]
  rw [CategoryTheory.Functor.map_id, Category.id_comp]
  erw [Category.id_comp]
  rfl

lemma singleFunctorsPostcompQIso_inv_hom (n : ‚Ñ§) :
    (singleFunctorsPostcompQIso C).inv.hom n = ùüô _ := by
  ext X
  simp [singleFunctorsPostcompQIso, HomotopyCategory.singleFunctorsPostcompQuotientIso]
  rfl

/-- The isomorphism `singleFunctor C n ‚âÖ CochainComplex.singleFunctor C n ‚ãô Q`. -/
noncomputable def singleFunctorIsoCompQ (n : ‚Ñ§) :
    singleFunctor C n ‚âÖ CochainComplex.singleFunctor C n ‚ãô Q := Iso.refl _

lemma isIso_Q_map_iff_quasiIso {K L : CochainComplex C ‚Ñ§} (œÜ : K ‚ü∂ L) :
    IsIso (Q.map œÜ) ‚Üî QuasiIso œÜ := by
  apply HomologicalComplexUpToQuasiIso.isIso_Q_map_iff_mem_quasiIso

end DerivedCategory
