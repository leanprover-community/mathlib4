import Mathlib.Algebra.Homology.HomotopyCategory.HomologicalFunctor
import Mathlib.Algebra.Homology.HomotopyCategory.ShiftSequence
import Mathlib.Algebra.Homology.HomotopyCategory.ShortExact
import Mathlib.Algebra.Homology.HomotopyCategory.Triangulated
import Mathlib.Algebra.Homology.HomotopyCategory.Cylinder
import Mathlib.CategoryTheory.Localization.Composition
import Mathlib.CategoryTheory.Localization.HasLocalization

open CategoryTheory Category Limits Pretriangulated ZeroObject Preadditive

universe w v u

variable (C : Type u) [Category.{v} C] [Abelian C]

namespace HomotopyCategory

def subcategoryAcyclic :
    Triangulated.Subcategory (HomotopyCategory C (ComplexShape.up ‚Ñ§)) :=
  Functor.homologicalKernel (newHomologyFunctor C (ComplexShape.up ‚Ñ§) 0)

lemma mem_subCategoryAcyclic_iff (X : HomotopyCategory C (ComplexShape.up ‚Ñ§)) :
    X ‚àà (subcategoryAcyclic C).set ‚Üî ‚àÄ (n : ‚Ñ§), IsZero ((newHomologyFunctor _ _ n).obj X) :=
  Functor.mem_homologicalKernel_iff _ X

def qis : MorphismProperty (HomotopyCategory C (ComplexShape.up ‚Ñ§)) := (subcategoryAcyclic C).W

lemma mem_qis_iff {X Y : HomotopyCategory C (ComplexShape.up ‚Ñ§)} (f : X ‚ü∂ Y) :
    qis _ f ‚Üî ‚àÄ (n : ‚Ñ§), IsIso ((newHomologyFunctor _ _ n).map f) := by
  dsimp only [qis, subcategoryAcyclic]
  rw [‚Üê Functor.IsHomological.W_eq_homologicalKernelW]
  apply Functor.IsHomological.mem_W_iff

lemma mem_qis_iff' {X Y : CochainComplex C ‚Ñ§} (f : X ‚ü∂ Y) :
    qis C ((quotient _ _).map f) ‚Üî
    ‚àÄ (n : ‚Ñ§), IsIso ((HomologicalComplex.newHomologyFunctor _ _ n).map f) := by
  simp only [mem_qis_iff,
    ‚Üê fun n => NatIso.isIso_map_iff (newHomologyFunctorFactors C (ComplexShape.up ‚Ñ§) n) f]
  rfl

end HomotopyCategory

abbrev HasDerivedCategory := MorphismProperty.HasLocalization.{w} (HomotopyCategory.qis C)

variable [HasDerivedCategory.{w} C]

def DerivedCategory := (HomotopyCategory.qis C).Localization'

namespace DerivedCategory

instance : Category (DerivedCategory C) := by
  dsimp only [DerivedCategory]
  infer_instance

noncomputable instance : Preadditive (DerivedCategory C) := by
  dsimp only [DerivedCategory, HomotopyCategory.qis]
  infer_instance

instance : HasZeroObject (DerivedCategory C) := by
  dsimp only [DerivedCategory, HomotopyCategory.qis]
  infer_instance

noncomputable instance : HasShift (DerivedCategory C) ‚Ñ§ := by
  dsimp only [DerivedCategory, HomotopyCategory.qis]
  infer_instance

instance (n : ‚Ñ§) : (shiftFunctor (DerivedCategory C) n).Additive := by
  dsimp only [DerivedCategory, HomotopyCategory.qis]
  infer_instance

noncomputable instance : Pretriangulated (DerivedCategory C) := by
  dsimp only [DerivedCategory, HomotopyCategory.qis]
  infer_instance

noncomputable instance : IsTriangulated (DerivedCategory C) := by
  dsimp only [DerivedCategory, HomotopyCategory.qis]
  infer_instance

variable {C}

def Qh : HomotopyCategory C (ComplexShape.up ‚Ñ§) ‚•§ DerivedCategory C :=
  MorphismProperty.Q' _

instance : Qh.IsLocalization (HomotopyCategory.qis C) := by
  dsimp only [Qh, DerivedCategory]
  infer_instance

instance : (HomotopyCategory.qis C).HasLeftCalculusOfFractions := by
  dsimp only [HomotopyCategory.qis]
  infer_instance

instance : (HomotopyCategory.qis C).HasRightCalculusOfFractions := by
  dsimp only [HomotopyCategory.qis]
  infer_instance

noncomputable instance : (Qh : _ ‚•§ DerivedCategory C).CommShift ‚Ñ§ := by
  dsimp only [Qh, DerivedCategory]
  infer_instance

instance : (Qh : _ ‚•§ DerivedCategory C).IsTriangulated := by
  dsimp only [Qh, DerivedCategory, HomotopyCategory.qis]
  infer_instance

instance : EssSurj (Functor.mapArrow (Qh : _ ‚•§ DerivedCategory C)) := by
  dsimp only [Qh, DerivedCategory, HomotopyCategory.qis]
  infer_instance

instance : EssSurj (Qh : _ ‚•§ DerivedCategory C) :=
  Localization.essSurj _ (HomotopyCategory.qis _)

def Q : CochainComplex C ‚Ñ§ ‚•§ DerivedCategory C :=
  (HomotopyCategory.quotient _ _ ) ‚ãô Qh

instance : EssSurj (Q : _ ‚•§ DerivedCategory C) := by
  dsimp only [Q]
  infer_instance

instance : (Q : CochainComplex C ‚Ñ§ ‚•§ _).Additive := by
  dsimp only [Q]
  infer_instance

noncomputable instance : (Q : CochainComplex C ‚Ñ§ ‚•§ _).CommShift ‚Ñ§ := by
  dsimp only [Q]
  infer_instance

@[reassoc]
lemma Q_commShiftIso_hom_app (X : CochainComplex C ‚Ñ§) (n : ‚Ñ§) :
    (Q.commShiftIso n).hom.app X =
      Qh.map (((HomotopyCategory.quotient C _).commShiftIso n).hom.app X) ‚â´
        (Qh.commShiftIso n).hom.app ((HomotopyCategory.quotient C (ComplexShape.up ‚Ñ§)).obj X) :=
  by apply Functor.commShiftIso_comp_hom_app

@[reassoc]
lemma Q_commShiftIso_inv_app (X : CochainComplex C ‚Ñ§) (n : ‚Ñ§) :
    (Q.commShiftIso n).inv.app X =
      (Qh.commShiftIso n).inv.app ((HomotopyCategory.quotient C (ComplexShape.up ‚Ñ§)).obj X) ‚â´
      Qh.map (((HomotopyCategory.quotient C _).commShiftIso n).inv.app X) :=
  by apply Functor.commShiftIso_comp_inv_app

@[reassoc]
lemma Qh_commShiftIso_hom_app (X : CochainComplex C ‚Ñ§) (n : ‚Ñ§) :
    Qh.map (((HomotopyCategory.quotient C _).commShiftIso n).hom.app X) =
      (Q.commShiftIso n).hom.app X ‚â´
        (Qh.commShiftIso n).inv.app ((HomotopyCategory.quotient C (ComplexShape.up ‚Ñ§)).obj X) := by
  simp only [Q_commShiftIso_hom_app, Functor.comp_obj, assoc, Iso.hom_inv_id_app, comp_id]

@[reassoc]
lemma Qh_commShiftIso_inv_app (X : CochainComplex C ‚Ñ§) (n : ‚Ñ§) :
    (Qh.commShiftIso n).inv.app ((HomotopyCategory.quotient C (ComplexShape.up ‚Ñ§)).obj X) =
      (Q.commShiftIso n).inv.app X ‚â´
      Qh.map (((HomotopyCategory.quotient C _).commShiftIso n).hom.app X) := by
  simp only [Q_commShiftIso_inv_app, assoc, ‚Üê Functor.map_comp, Iso.inv_hom_id_app,
    Functor.comp_obj, Paths.of_obj, CategoryTheory.Functor.map_id, comp_id]

lemma mem_distTriang_iff (T : Triangle (DerivedCategory C)) :
    (T ‚àà distTriang (DerivedCategory C)) ‚Üî ‚àÉ (X Y : CochainComplex C ‚Ñ§) (f : X ‚ü∂ Y),
      Nonempty (T ‚âÖ Q.mapTriangle.obj (CochainComplex.MappingCone.triangle f)) := by
  constructor
  . rintro ‚ü®T', e, ‚ü®X, Y, f, ‚ü®e'‚ü©‚ü©‚ü©
    exact ‚ü®_, _, f, ‚ü®e ‚â™‚â´ Qh.mapTriangle.mapIso e' ‚â™‚â´
      (Functor.mapTriangleCompIso (HomotopyCategory.quotient C _) Qh).symm.app _‚ü©‚ü©
  . rintro ‚ü®X, Y, f, ‚ü®e‚ü©‚ü©
    refine' isomorphic_distinguished _ (Qh.map_distinguished _ _) _
      (e ‚â™‚â´ (Functor.mapTriangleCompIso (HomotopyCategory.quotient C _) Qh).app _)
    exact ‚ü®_, _, f, ‚ü®Iso.refl _‚ü©‚ü©

lemma induction_Q_obj (P : DerivedCategory C ‚Üí Prop)
    (hP‚ÇÅ : ‚àÄ (X : CochainComplex C ‚Ñ§), P (Q.obj X))
    (hP‚ÇÇ : ‚àÄ ‚¶ÉX Y : DerivedCategory C‚¶Ñ (_ : X ‚âÖ Y), P X ‚Üí P Y)
    (X : DerivedCategory C) : P X :=
  hP‚ÇÇ (Q.objObjPreimageIso X) (hP‚ÇÅ _)

variable (C)

-- this should be moved (and generalized)
instance : (HomologicalComplex.single C (ComplexShape.up ‚Ñ§) n).Additive where

noncomputable def singleFunctor (n : ‚Ñ§) : C ‚•§ DerivedCategory C :=
  HomologicalComplex.single _ _ n ‚ãô Q

instance (n : ‚Ñ§) : (singleFunctor C n).Additive := by
  dsimp only [singleFunctor]
  infer_instance

noncomputable def singleFunctorShiftIso (n a a' : ‚Ñ§) (ha' : n + a = a') :
    singleFunctor C a' ‚ãô shiftFunctor _ n ‚âÖ singleFunctor C a :=
  Functor.associator _ _ _ ‚â™‚â´ isoWhiskerLeft _ (Q.commShiftIso n).symm ‚â™‚â´
    (Functor.associator _ _ _).symm ‚â™‚â´
    isoWhiskerRight (CochainComplex.singleShiftIso C n a a' ha') Q

lemma homologyFunctor_inverts_qis (n : ‚Ñ§) :
    (HomotopyCategory.qis C).IsInvertedBy
      (HomotopyCategory.newHomologyFunctor C _ n) := fun X Y f hf => by
  rw [HomotopyCategory.mem_qis_iff] at hf
  exact hf n

noncomputable def homologyFunctor (n : ‚Ñ§) : DerivedCategory C ‚•§ C :=
  Localization.lift _ (homologyFunctor_inverts_qis C n) Qh

noncomputable def homologyFunctorFactorsh (n : ‚Ñ§) : Qh ‚ãô homologyFunctor C n ‚âÖ
  HomotopyCategory.newHomologyFunctor C _ n := Localization.fac _ _ _

attribute [irreducible] homologyFunctor homologyFunctorFactorsh

noncomputable def homologyFunctorFactors (n : ‚Ñ§) : Q ‚ãô homologyFunctor C n ‚âÖ
    HomologicalComplex.newHomologyFunctor _ _ n :=
  Functor.associator _ _ _ ‚â™‚â´
    isoWhiskerLeft _ (homologyFunctorFactorsh C n) ‚â™‚â´
    HomotopyCategory.newHomologyFunctorFactors _ _ _

noncomputable def singleFunctorCompHomologyFunctorIso (n : ‚Ñ§) :
    singleFunctor C n ‚ãô homologyFunctor C n ‚âÖ ùü≠ C :=
  Functor.associator _ _ _ ‚â™‚â´ isoWhiskerLeft _ (homologyFunctorFactors C n) ‚â™‚â´
    HomologicalComplex.singleCompHomologyFunctorIso C (ComplexShape.up ‚Ñ§) n

instance : (homologyFunctor C n).PreservesZeroMorphisms :=
  Functor.preservesZeroMorphisms_of_fac_of_essSurj _ _ _
    (homologyFunctorFactorsh C n)

-- could be better to have `IsHomological` extend `PreservesZeroMorphisms` so that
-- we do not have to prove both statement separately
instance : (homologyFunctor C n).IsHomological :=
  Functor.isHomological_of_localization Qh
    (homologyFunctor C n) _ (homologyFunctorFactorsh C n)

noncomputable instance :
    (homologyFunctor C 0).ShiftSequence ‚Ñ§ :=
  Functor.ShiftSequence.induced (homologyFunctorFactorsh C 0) ‚Ñ§
    (homologyFunctor C) (homologyFunctorFactorsh C)
      ‚ü®‚ü®(inferInstance :
          Full (Localization.whiskeringLeftFunctor' Qh (HomotopyCategory.qis C) C))‚ü©,
        (inferInstance :
          Faithful (Localization.whiskeringLeftFunctor' Qh (HomotopyCategory.qis C) C))‚ü©

variable {C}

lemma isIso_Qh_map_iff {X Y : HomotopyCategory C (ComplexShape.up ‚Ñ§)} (f : X ‚ü∂ Y) :
    IsIso (Qh.map f) ‚Üî HomotopyCategory.qis C f := by
  constructor
  . intro hf
    rw [HomotopyCategory.mem_qis_iff]
    intro n
    rw [‚Üê NatIso.isIso_map_iff (homologyFunctorFactorsh C n) f]
    dsimp
    infer_instance
  . intro hf
    exact Localization.inverts Qh (HomotopyCategory.qis C) _ hf

lemma isIso_Q_map_iff {K L : CochainComplex C ‚Ñ§} (œÜ : K ‚ü∂ L) :
    IsIso (Q.map œÜ) ‚Üî
      ‚àÄ (n : ‚Ñ§), IsIso ((HomologicalComplex.newHomologyFunctor C _ n).map œÜ) := by
  dsimp only [Q, Functor.comp]
  rw [‚Üê HomotopyCategory.mem_qis_iff', isIso_Qh_map_iff]

lemma isIso_Q_map_iff' {K L : CochainComplex C ‚Ñ§} (œÜ : K ‚ü∂ L) :
    IsIso (Q.map œÜ) ‚Üî HomologicalComplex.qis _ _ œÜ :=
  isIso_Q_map_iff œÜ

instance : Q.IsLocalization (HomologicalComplex.qis C (ComplexShape.up ‚Ñ§)) := by
  refine' Functor.IsLocalization.comp (HomotopyCategory.quotient _ _)
    (HomologicalComplex.homotopyEquivalences _ _) Qh (HomotopyCategory.qis C) _ _ _ _
  . intro X Y f hf
    exact (isIso_Q_map_iff f).2 hf
  . apply HomologicalComplex.homotopyEquivalences_subset_qis
  . rintro ‚ü®K : CochainComplex C ‚Ñ§‚ü© ‚ü®L : CochainComplex C ‚Ñ§‚ü© f hf
    obtain ‚ü®f, rfl‚ü© := (HomotopyCategory.quotient _ _).map_surjective f
    apply MorphismProperty.map_mem_map
    simpa only [HomotopyCategory.mem_qis_iff'] using hf

lemma isIso_iff {K L : DerivedCategory C} (f : K ‚ü∂ L) :
    IsIso f ‚Üî ‚àÄ (n : ‚Ñ§), IsIso ((homologyFunctor C n).map f) := by
  constructor
  . intro hf n
    infer_instance
  . intro hf
    let g := (Functor.mapArrow Qh).objPreimage (Arrow.mk f)
    refine' ((MorphismProperty.RespectsIso.isomorphisms (DerivedCategory C)).arrow_iso_iff
      ((Functor.mapArrow Qh).objObjPreimageIso (Arrow.mk f))).1 _
    change IsIso (Qh.map g.hom)
    rw [isIso_Qh_map_iff, HomotopyCategory.mem_qis_iff]
    intro n
    have e : Arrow.mk ((homologyFunctor C n).map f) ‚âÖ
        Arrow.mk ((HomotopyCategory.newHomologyFunctor _ _ n).map g.hom) :=
      ((homologyFunctor C n).mapArrow.mapIso
        (((Functor.mapArrow Qh).objObjPreimageIso (Arrow.mk f)).symm)) ‚â™‚â´
        ((Functor.mapArrowFunctor _ _).mapIso (homologyFunctorFactorsh C n)).app (Arrow.mk g.hom)
    exact ((MorphismProperty.RespectsIso.isomorphisms C).arrow_iso_iff e).1 (hf n)

lemma isZero_iff (K : DerivedCategory C) :
    IsZero K ‚Üî ‚àÄ (n : ‚Ñ§), IsZero ((homologyFunctor C n).obj K) := by
  constructor
  . intro hK n
    rw [IsZero.iff_id_eq_zero, ‚Üê ((homologyFunctor C n).map_id K),
      (IsZero.iff_id_eq_zero K).1 hK, Functor.map_zero]
  . intro hK
    have : IsIso (0 : K ‚ü∂ 0) := by
      rw [isIso_iff]
      intro n
      refine' ‚ü®0, _, _‚ü©
      . apply (hK n).eq_of_src
      . rw [zero_comp, ‚Üê (homologyFunctor C n).map_id, id_zero,
          Functor.map_zero]
    exact IsZero.of_iso (isZero_zero _) (asIso (0 : K ‚ü∂ 0))

section

variable {S : ShortComplex (CochainComplex C ‚Ñ§)} (hS : S.ShortExact)

lemma isIso_Q_map_fromOfShortComplex :
    IsIso (Q.map (CochainComplex.MappingCone.fromOfShortComplex S)) := by
  rw [isIso_Q_map_iff]
  exact CochainComplex.MappingCone.isIso_homologyMap_fromOfShortComplex hS

noncomputable def triangleOfSESŒ¥ :
  Q.obj (S.X‚ÇÉ) ‚ü∂ (Q.obj S.X‚ÇÅ)‚ü¶(1 : ‚Ñ§)‚üß :=
    have := isIso_Q_map_fromOfShortComplex hS
    inv (Q.map (CochainComplex.MappingCone.fromOfShortComplex S)) ‚â´
      Q.map (CochainComplex.MappingCone.triangleŒ¥ S.f) ‚â´
      (Q.commShiftIso (1 : ‚Ñ§)).hom.app S.X‚ÇÅ

noncomputable def triangleOfSES : Triangle (DerivedCategory C) :=
  Triangle.mk (Q.map S.f) (Q.map S.g) (triangleOfSESŒ¥ hS)

noncomputable def triangleOfSESIso :
    Q.mapTriangle.obj (CochainComplex.MappingCone.triangle S.f) ‚âÖ triangleOfSES hS := by
  have := isIso_Q_map_fromOfShortComplex hS
  refine' Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _)
    (asIso (Q.map (CochainComplex.MappingCone.fromOfShortComplex S))) _ _ _
  . dsimp [triangleOfSES]
    simp only [comp_id, id_comp]
  . dsimp [triangleOfSES, CochainComplex.MappingCone.fromOfShortComplex, asIso]
    rw [id_comp, ‚Üê Q.map_comp, CochainComplex.MappingCone.inr_desc]
  . dsimp [triangleOfSES, triangleOfSESŒ¥]
    rw [CategoryTheory.Functor.map_id, comp_id, IsIso.hom_inv_id_assoc]

lemma triangleOfSES_distinguished :
    triangleOfSES hS ‚àà distTriang (DerivedCategory C) := by
  rw [mem_distTriang_iff]
  exact ‚ü®_, _, S.f, ‚ü®(triangleOfSESIso hS).symm‚ü©‚ü©

namespace HomologySequence

variable (T : Triangle (DerivedCategory C)) (hT : T ‚àà distTriang _)
  (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (h : n‚ÇÄ + 1 = n‚ÇÅ)

noncomputable def Œ¥ : (homologyFunctor C n‚ÇÄ).obj T.obj‚ÇÉ ‚ü∂ (homologyFunctor C n‚ÇÅ).obj T.obj‚ÇÅ :=
  (homologyFunctor C 0).shiftMap T.mor‚ÇÉ n‚ÇÄ n‚ÇÅ (by rw [add_comm 1, h])

lemma comp_Œ¥ : (homologyFunctor C n‚ÇÄ).map T.mor‚ÇÇ ‚â´ Œ¥ T n‚ÇÄ n‚ÇÅ h = 0 :=
  (homologyFunctor C 0).comp_homology_sequence_Œ¥ _ hT _ _ h

lemma Œ¥_comp : Œ¥ T n‚ÇÄ n‚ÇÅ h ‚â´ (homologyFunctor C n‚ÇÅ).map T.mor‚ÇÅ = 0 :=
  (homologyFunctor C 0).homology_sequence_Œ¥_comp _ hT _ _ h

lemma exact‚ÇÇ :
  (ShortComplex.mk ((homologyFunctor C n‚ÇÄ).map T.mor‚ÇÅ) ((homologyFunctor C n‚ÇÄ).map T.mor‚ÇÇ)
    (by simp only [‚Üê Functor.map_comp, comp_dist_triangle_mor_zero‚ÇÅ‚ÇÇ _ hT,
      Functor.map_zero])).Exact :=
  (homologyFunctor C 0).homology_sequence_exact‚ÇÇ _ hT _

lemma exact‚ÇÉ :
  (ShortComplex.mk _ _ (comp_Œ¥ T hT n‚ÇÄ n‚ÇÅ h)).Exact :=
  (homologyFunctor C 0).homology_sequence_exact‚ÇÉ _ hT _ _ h

lemma exact‚ÇÅ :
  (ShortComplex.mk _ _ (Œ¥_comp T hT n‚ÇÄ n‚ÇÅ h)).Exact :=
  (homologyFunctor C 0).homology_sequence_exact‚ÇÅ _ hT _ _ h

lemma epi_homologyMap_mor‚ÇÅ_iff :
    Epi ((homologyFunctor C n‚ÇÄ).map T.mor‚ÇÅ) ‚Üî (homologyFunctor C n‚ÇÄ).map T.mor‚ÇÇ = 0 :=
  (homologyFunctor C 0).homology_sequence_epi_shift_map_mor‚ÇÅ_iff _ hT _

lemma mono_homologyMap_mor‚ÇÅ_iff :
    Mono ((homologyFunctor C n‚ÇÅ).map T.mor‚ÇÅ) ‚Üî Œ¥ T n‚ÇÄ n‚ÇÅ h  = 0 :=
  (homologyFunctor C 0).homology_sequence_mono_shift_map_mor‚ÇÅ_iff _ hT _ _ h

lemma isIso_homologyMap_mor‚ÇÅ_iff :
    IsIso ((homologyFunctor C n‚ÇÅ).map T.mor‚ÇÅ) ‚Üî
      Œ¥ T n‚ÇÄ n‚ÇÅ h  = 0 ‚àß (homologyFunctor C n‚ÇÅ).map T.mor‚ÇÇ = 0 :=
  (homologyFunctor C 0).homology_sequence_isIso_shift_map_mor‚ÇÅ_iff _ hT _ _ h

end HomologySequence

end

lemma right_fac (X Y : CochainComplex C ‚Ñ§) (f : Q.obj X ‚ü∂ Q.obj Y) :
    ‚àÉ (X' : CochainComplex C ‚Ñ§) (s : X' ‚ü∂ X) (hs : IsIso (Q.map s)) (g : X' ‚ü∂ Y),
      f = inv (Q.map s) ‚â´ Q.map g := by
  dsimp only [Q, Functor.comp] at f
  obtain ‚ü®X', g', s', hs', fac‚ü© :=
    MorphismProperty.HasRightCalculusOfFractions.fac Qh (HomotopyCategory.qis C) f
  rw [‚Üê isIso_Qh_map_iff] at hs'
  obtain ‚ü®X', rfl‚ü© := HomotopyCategory.quotient_obj_surjective X'
  obtain ‚ü®g, rfl‚ü© := (HomotopyCategory.quotient _ _).map_surjective g'
  obtain ‚ü®s, rfl‚ü© := (HomotopyCategory.quotient _ _).map_surjective s'
  exact ‚ü®X', s, hs', g, fac‚ü©

lemma left_fac (X Y : CochainComplex C ‚Ñ§) (f : Q.obj X ‚ü∂ Q.obj Y) :
    ‚àÉ (Y' : CochainComplex C ‚Ñ§) (g : X ‚ü∂ Y') (s : Y ‚ü∂ Y') (hs : IsIso (Q.map s)),
      f = Q.map g ‚â´ inv (Q.map s) := by
  dsimp only [Q, Functor.comp] at f
  obtain ‚ü®X', g', s', hs', fac‚ü© :=
    MorphismProperty.HasLeftCalculusOfFractions.fac Qh (HomotopyCategory.qis C) f
  rw [‚Üê isIso_Qh_map_iff] at hs'
  obtain ‚ü®X', rfl‚ü© := HomotopyCategory.quotient_obj_surjective X'
  obtain ‚ü®g, rfl‚ü© := (HomotopyCategory.quotient _ _).map_surjective g'
  obtain ‚ü®s, rfl‚ü© := (HomotopyCategory.quotient _ _).map_surjective s'
  exact ‚ü®X', g, s, hs', fac‚ü©

end DerivedCategory
