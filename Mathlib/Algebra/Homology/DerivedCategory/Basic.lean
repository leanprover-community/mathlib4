/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Homology.HomotopyCategory.HomologicalFunctor
import Mathlib.Algebra.Homology.HomotopyCategory.ShiftSequence
import Mathlib.Algebra.Homology.HomotopyCategory.ShortExact
import Mathlib.Algebra.Homology.HomotopyCategory.SingleFunctors
import Mathlib.Algebra.Homology.HomotopyCategory.Triangulated
import Mathlib.Algebra.Homology.Localization

/-! # The derived category of an abelian category

In this file, we construct the derived category `DerivedCategory C` of an
abelian category `C`. If is equipped with a triangulated structure.

The derived category is defined here as the localization of cochain complexes
indexed by `‚Ñ§` with respect to quasi-isomorphisms: it is a type synonym of
`HomologicalComplexUpToQuasiIso C (ComplexShape.up ‚Ñ§)`. Then, we have a
localization functor `DerivedCategory.Q : CochainComplex C ‚Ñ§ ‚•§ DerivedCategory C`.
It was already shown in the file `Algebra.Homology.Localization` that the induced
functor `DerivedCategory.Qh : HomotopyCategory C (ComplexShape.up ‚Ñ§) ‚•§ DerivedCategory C`
is a localization functor with respect to the class of morphisms
`HomotopyCategory.quasiIso C (ComplexShape.up ‚Ñ§)`. In the lemma
`HomotopyCategory.quasiIso_eq_subcategoryAcyclic_W` we obtain that this class of morphisms
consists of morphisms whose cone belongs to the triangulated subcategory
`HomotopyCategory.subcategoryAcyclic C` of acyclic complexes. Then, the triangulated
structure on `DerivedCategory C` is deduced from the triangulated structure
on the homotopy category (see file `Algebra.Homology.HomotopyCategory.Triangulated`)
using the localization theorem for triangulated categories which was obtained
in the file `CategoryTheory.Localization.Triangulated`.

## Implementation notes

If `C : Type u` and `Category.{v} C`, the constructed localized category of cochain
complexes with respect to quasi-isomorphisms has morphisms in `Type (max u v)`.
However, in certain circumstances, it shall be possible to prove that they are `v`-small
(when `C` is a Grothendieck abelian category (e.g. the category of modules over a ring),
it should be so by a theorem of Hovey.).

Then, when working with derived categories in mathlib, the user should add the variable
`[HasDerivedCategory.{w} C]` which is the assumption that there is a chosen derived
category with morphisms in `Type w`. When derived categories are used in order to
prove statements which do not involve derived categories, the `HasDerivedCategory.{max u v}`
instance should be obtained at the beginning of the proof, using the term
`HasDerivedCategory.standard C`.

## TODO (@joelriou)

- define the induced homological functor `DerivedCategory C ‚•§ C`.
- construct the distinguished triangle associated to a short exact sequence
of cochain complexes, and compare the associated connecting homomorphism
with the one defined in `Algebra.Homology.HomologySequence`.
- refactor the definition of Ext groups using morphisms in the derived category
(which may be shrunk to the universe `v` at least when `C` has enough projectives
or enough injectives).

## References
* [Jean-Louis Verdier, *Des cat√©gories d√©riv√©es des cat√©gories ab√©liennes*][verdier1996]
* [Mark Hovey, *Model category structures on chain complexes of sheaves*][hovey-2001]

-/

universe w v u

open CategoryTheory Category Limits Pretriangulated ZeroObject

variable (C : Type u) [Category.{v} C] [Abelian C]

namespace HomotopyCategory

/-- The triangulated subcategory of `HomotopyCategory C (ComplexShape.up ‚Ñ§)` consisting
of acyclic complexes. -/
def subcategoryAcyclic : Triangulated.Subcategory (HomotopyCategory C (ComplexShape.up ‚Ñ§)) :=
  (homologyFunctor C (ComplexShape.up ‚Ñ§) 0).homologicalKernel

instance : ClosedUnderIsomorphisms (subcategoryAcyclic C).P := by
  dsimp [subcategoryAcyclic]
  infer_instance

variable {C}

lemma mem_subcategoryAcyclic_iff (X : HomotopyCategory C (ComplexShape.up ‚Ñ§)) :
    (subcategoryAcyclic C).P X ‚Üî ‚àÄ (n : ‚Ñ§), IsZero ((homologyFunctor _ _ n).obj X) :=
  Functor.mem_homologicalKernel_iff _ X

lemma quotient_obj_mem_subcategoryAcyclic_iff_exactAt (K : CochainComplex C ‚Ñ§) :
    (subcategoryAcyclic C).P ((quotient _ _).obj K) ‚Üî ‚àÄ (n : ‚Ñ§), K.ExactAt n := by
  rw [mem_subcategoryAcyclic_iff]
  refine forall_congr' (fun n => ?_)
  simp only [HomologicalComplex.exactAt_iff_isZero_homology]
  exact ((homologyFunctorFactors C (ComplexShape.up ‚Ñ§) n).app K).isZero_iff

variable (C)

lemma quasiIso_eq_subcategoryAcyclic_W :
    quasiIso C (ComplexShape.up ‚Ñ§) = (subcategoryAcyclic C).W := by
  ext K L f
  exact ((homologyFunctor C (ComplexShape.up ‚Ñ§) 0).mem_homologicalKernel_W_iff f).symm

end HomotopyCategory

/-- The assumption that a localized category for
`(HomologicalComplex.quasiIso C (ComplexShape.up ‚Ñ§))` has been chosen, and that the morphisms
in this chosen category are in `Type w`. -/
abbrev HasDerivedCategory := MorphismProperty.HasLocalization.{w}
  (HomologicalComplex.quasiIso C (ComplexShape.up ‚Ñ§))

/-- The derived category obtained using the constructed localized category of cochain complexes
with respect to quasi-isomorphisms. This should be used only while proving statements
which do not involve the derived category. -/
def HasDerivedCategory.standard : HasDerivedCategory.{max u v} C :=
  MorphismProperty.HasLocalization.standard _

variable [HasDerivedCategory.{w} C]

/-- The derived category of an abelian category. -/
def DerivedCategory := HomologicalComplexUpToQuasiIso C (ComplexShape.up ‚Ñ§)

namespace DerivedCategory

instance : Category (DerivedCategory C) := by
  dsimp [DerivedCategory]
  infer_instance

variable {C}

/-- The localization functor `CochainComplex C ‚Ñ§ ‚•§ DerivedCategory C`. -/
def Q : CochainComplex C ‚Ñ§ ‚•§ DerivedCategory C := HomologicalComplexUpToQuasiIso.Q

instance : (Q (C := C)).IsLocalization
    (HomologicalComplex.quasiIso C (ComplexShape.up ‚Ñ§)) := by
  dsimp only [Q, DerivedCategory]
  infer_instance

/-- The localization functor `HomotopyCategory C (ComplexShape.up ‚Ñ§) ‚•§ DerivedCategory C`. -/
def Qh : HomotopyCategory C (ComplexShape.up ‚Ñ§) ‚•§ DerivedCategory C :=
  HomologicalComplexUpToQuasiIso.Qh

variable (C) in
/-- The natural isomorphism `HomotopyCategory.quotient C (ComplexShape.up ‚Ñ§) ‚ãô Qh ‚âÖ Q`. -/
def quotientCompQhIso : HomotopyCategory.quotient C (ComplexShape.up ‚Ñ§) ‚ãô Qh ‚âÖ Q :=
  HomologicalComplexUpToQuasiIso.quotientCompQhIso C (ComplexShape.up ‚Ñ§)

instance : Qh.IsLocalization (HomotopyCategory.quasiIso C (ComplexShape.up ‚Ñ§)) := by
  dsimp [Qh, DerivedCategory]
  infer_instance

instance : Qh.IsLocalization (HomotopyCategory.subcategoryAcyclic C).W := by
  rw [‚Üê HomotopyCategory.quasiIso_eq_subcategoryAcyclic_W]
  infer_instance

noncomputable instance : Preadditive (DerivedCategory C) :=
  Localization.preadditive Qh (HomotopyCategory.subcategoryAcyclic C).W

instance : (Qh (C := C)).Additive :=
  Localization.functor_additive Qh (HomotopyCategory.subcategoryAcyclic C).W

instance : (Q (C := C)).Additive :=
  Functor.additive_of_iso (quotientCompQhIso C)

noncomputable instance : HasZeroObject (DerivedCategory C) :=
  Q.hasZeroObject_of_additive

noncomputable instance : HasShift (DerivedCategory C) ‚Ñ§ :=
  HasShift.localized Qh (HomotopyCategory.subcategoryAcyclic C).W ‚Ñ§

noncomputable instance : (Qh (C := C)).CommShift ‚Ñ§ :=
  Functor.CommShift.localized Qh (HomotopyCategory.subcategoryAcyclic C).W ‚Ñ§

instance shiftFunctor_additive (n : ‚Ñ§) : (shiftFunctor (DerivedCategory C) n).Additive := by
  rw [Localization.functor_additive_iff
    Qh (HomotopyCategory.subcategoryAcyclic C).W]
  exact Functor.additive_of_iso (Qh.commShiftIso n)

noncomputable instance : Pretriangulated (DerivedCategory C) :=
  Triangulated.Localization.pretriangulated
    Qh (HomotopyCategory.subcategoryAcyclic C).W

instance : (Qh (C := C)).IsTriangulated :=
  Triangulated.Localization.isTriangulated_functor
    Qh (HomotopyCategory.subcategoryAcyclic C).W

noncomputable instance : IsTriangulated (DerivedCategory C) :=
  Triangulated.Localization.isTriangulated
    Qh (HomotopyCategory.subcategoryAcyclic C).W

noncomputable instance (n : ‚Ñ§) :
  Localization.Lifting Qh (HomotopyCategory.subcategoryAcyclic C).W
    (shiftFunctor (HomotopyCategory C (ComplexShape.up ‚Ñ§)) n ‚ãô Qh)
    (shiftFunctor (DerivedCategory C) n) := ‚ü®(Qh.commShiftIso n).symm‚ü©

instance : EssSurj (Qh : _ ‚•§ DerivedCategory C).mapArrow :=
  Triangulated.Localization.essSurj_mapArrow
    Qh (HomotopyCategory.subcategoryAcyclic C).W

instance : EssSurj (Q : _ ‚•§ DerivedCategory C).mapArrow where
  mem_essImage œÜ := by
    obtain ‚ü®‚ü®K‚ü©, ‚ü®L‚ü©, f, ‚ü®e‚ü©‚ü© :
        ‚àÉ (K L : HomotopyCategory C (ComplexShape.up ‚Ñ§)) (f : K ‚ü∂ L),
          Nonempty (Arrow.mk (Qh.map f) ‚âÖ œÜ) := ‚ü®_, _, _, ‚ü®Qh.mapArrow.objObjPreimageIso œÜ‚ü©‚ü©
    obtain ‚ü®f, rfl‚ü© := (HomotopyCategory.quotient C (ComplexShape.up ‚Ñ§)).map_surjective f
    exact ‚ü®Arrow.mk f, ‚ü®e‚ü©‚ü©

instance : (HomotopyCategory.quasiIso C (ComplexShape.up ‚Ñ§)).HasLeftCalculusOfFractions := by
  rw [HomotopyCategory.quasiIso_eq_subcategoryAcyclic_W]
  infer_instance

instance : (HomotopyCategory.quasiIso C (ComplexShape.up ‚Ñ§)).HasRightCalculusOfFractions := by
  rw [HomotopyCategory.quasiIso_eq_subcategoryAcyclic_W]
  infer_instance

instance : EssSurj (Qh : _ ‚•§ DerivedCategory C) :=
  Localization.essSurj _ (HomotopyCategory.quasiIso _ _)

instance : EssSurj (Q : _ ‚•§ DerivedCategory C) :=
  Localization.essSurj _ (HomologicalComplex.quasiIso _ _)

noncomputable instance : (Q : CochainComplex C ‚Ñ§ ‚•§ _).CommShift ‚Ñ§ :=
  Functor.CommShift.ofIso (quotientCompQhIso C) ‚Ñ§

instance : NatTrans.CommShift (quotientCompQhIso C).hom ‚Ñ§ :=
  Functor.CommShift.ofIso_compatibility (quotientCompQhIso C) ‚Ñ§

lemma mem_distTriang_iff (T : Triangle (DerivedCategory C)) :
    (T ‚àà distTriang (DerivedCategory C)) ‚Üî ‚àÉ (X Y : CochainComplex C ‚Ñ§) (f : X ‚ü∂ Y),
      Nonempty (T ‚âÖ Q.mapTriangle.obj (CochainComplex.mappingCone.triangle f)) := by
  constructor
  ¬∑ rintro ‚ü®T', e, ‚ü®X, Y, f, ‚ü®e'‚ü©‚ü©‚ü©
    refine' ‚ü®_, _, f, ‚ü®_‚ü©‚ü©
    exact e ‚â™‚â´ Qh.mapTriangle.mapIso e' ‚â™‚â´
      (Functor.mapTriangleCompIso (HomotopyCategory.quotient C _) Qh).symm.app _ ‚â™‚â´
      (Functor.mapTriangleIso (quotientCompQhIso C)).app _
  ¬∑ rintro ‚ü®X, Y, f, ‚ü®e‚ü©‚ü©
    refine' isomorphic_distinguished _ (Qh.map_distinguished _ _) _
      (e ‚â™‚â´ (Functor.mapTriangleIso (quotientCompQhIso C)).symm.app _ ‚â™‚â´
      (Functor.mapTriangleCompIso (HomotopyCategory.quotient C _) Qh).app _)
    exact ‚ü®_, _, f, ‚ü®Iso.refl _‚ü©‚ü©

lemma induction_Q_obj (P : DerivedCategory C ‚Üí Prop)
    (hP‚ÇÅ : ‚àÄ (X : CochainComplex C ‚Ñ§), P (Q.obj X))
    (hP‚ÇÇ : ‚àÄ ‚¶ÉX Y : DerivedCategory C‚¶Ñ (_ : X ‚âÖ Y), P X ‚Üí P Y)
    (X : DerivedCategory C) : P X :=
  hP‚ÇÇ (Q.objObjPreimageIso X) (hP‚ÇÅ _)

variable (C)

noncomputable def singleFunctors : SingleFunctors C (DerivedCategory C) ‚Ñ§ :=
  (HomotopyCategory.singleFunctors C).postComp Qh

noncomputable abbrev singleFunctor (n : ‚Ñ§) := (singleFunctors C).functor n

instance (n : ‚Ñ§) : (singleFunctor C n).Additive := by
  dsimp [singleFunctor, singleFunctors]
  infer_instance

noncomputable def singleFunctorsPostCompQhIso :
    singleFunctors C ‚âÖ (HomotopyCategory.singleFunctors C).postComp Qh :=
  Iso.refl _

noncomputable def singleFunctorsPostCompQIso :
    singleFunctors C ‚âÖ (CochainComplex.singleFunctors C).postComp Q :=
  (SingleFunctors.postCompFunctor C ‚Ñ§ (Qh : _ ‚•§ DerivedCategory C)).mapIso (HomotopyCategory.singleFunctorsPostCompQuotientIso C)
    ‚â™‚â´ (CochainComplex.singleFunctors C).postCompPostCompIso (HomotopyCategory.quotient _ _) Qh ‚â™‚â´
      SingleFunctors.postCompIsoOfIso
        (CochainComplex.singleFunctors C) (quotientCompQhIso C)

/-noncomputable def singleFunctor (n : ‚Ñ§) : C ‚•§ DerivedCategory C :=
  HomologicalComplex.single _ _ n ‚ãô Q

instance (n : ‚Ñ§) : (singleFunctor C n).Additive := by
  dsimp only [singleFunctor]
  infer_instance

noncomputable def singleFunctorShiftIso (n a a' : ‚Ñ§) (ha' : n + a = a') :
    singleFunctor C a' ‚ãô shiftFunctor _ n ‚âÖ singleFunctor C a :=
  Functor.associator _ _ _ ‚â™‚â´ isoWhiskerLeft _ (Q.commShiftIso n).symm ‚â™‚â´
    (Functor.associator _ _ _).symm ‚â™‚â´
    isoWhiskerRight (CochainComplex.singleShiftIso C n a a' ha') Q

variable {C}

lemma singleFunctorShiftIso_hom_app (n a a' : ‚Ñ§) (ha' : n + a = a') (X : C) :
    (singleFunctorShiftIso C n a a' ha').hom.app X =
      (Q.commShiftIso n).inv.app ((HomologicalComplex.single C (ComplexShape.up ‚Ñ§) a').obj X) ‚â´
        Q.map ((CochainComplex.singleShiftIso C n a a' ha').hom.app X) := by
  dsimp [singleFunctorShiftIso]
  erw [id_comp, id_comp]

lemma singleFunctorShiftIso_inv_app (n a a' : ‚Ñ§) (ha' : n + a = a') (X : C) :
    (singleFunctorShiftIso C n a a' ha').inv.app X =
      Q.map ((CochainComplex.singleShiftIso C n a a' ha').inv.app X) ‚â´
      (Q.commShiftIso n).hom.app ((HomologicalComplex.single C (ComplexShape.up ‚Ñ§) a').obj X) := by
  dsimp [singleFunctorShiftIso]
  erw [comp_id, comp_id]-/


noncomputable def homologyFunctor (n : ‚Ñ§) : DerivedCategory C ‚•§ C :=
  HomologicalComplexUpToQuasiIso.homologyFunctor C (ComplexShape.up ‚Ñ§) n

noncomputable def homologyFunctorFactors (n : ‚Ñ§) : Q ‚ãô homologyFunctor C n ‚âÖ
    HomologicalComplex.homologyFunctor _ _ n :=
  HomologicalComplexUpToQuasiIso.homologyFunctorFactors C (ComplexShape.up ‚Ñ§) n

noncomputable def homologyFunctorFactorsh (n : ‚Ñ§) : Qh ‚ãô homologyFunctor C n ‚âÖ
    HomotopyCategory.homologyFunctor _ _ n :=
  HomologicalComplexUpToQuasiIso.homologyFunctorFactorsh C (ComplexShape.up ‚Ñ§) n


noncomputable def singleFunctorCompHomologyFunctorIso (n : ‚Ñ§) :
    singleFunctor C n ‚ãô homologyFunctor C n ‚âÖ ùü≠ C :=
  isoWhiskerRight ((SingleFunctors.evaluation _ _ n).mapIso (singleFunctorsPostCompQIso C)) _ ‚â™‚â´ Functor.associator _ _ _ ‚â™‚â´
    isoWhiskerLeft _ (homologyFunctorFactors C n) ‚â™‚â´
      HomologicalComplex.homologyFunctorSingleIso _ _ _

instance (n : ‚Ñ§) : (homologyFunctor C n).PreservesZeroMorphisms :=
  Functor.preservesZeroMorphisms_of_fac_of_essSurj _ _ _
    (homologyFunctorFactorsh C n)

-- could be better to have `IsHomological` extend `PreservesZeroMorphisms` so that
-- we do not have to prove both statement separately
instance (n : ‚Ñ§) : (homologyFunctor C n).IsHomological :=
  Functor.isHomological_of_localization Qh
    (homologyFunctor C n) _ (homologyFunctorFactorsh C n)

noncomputable instance :
    (homologyFunctor C 0).ShiftSequence ‚Ñ§ :=
  Functor.ShiftSequence.induced (homologyFunctorFactorsh C 0) ‚Ñ§
    (homologyFunctor C) (homologyFunctorFactorsh C)
      ‚ü®‚ü®(inferInstance :
          Full (Localization.whiskeringLeftFunctor' Qh (HomotopyCategory.quasiIso _ _) C))‚ü©,
        (inferInstance :
          Faithful (Localization.whiskeringLeftFunctor' Qh (HomotopyCategory.quasiIso _ _) C))‚ü©

variable {C}

lemma isIso_Qh_map_iff {X Y : HomotopyCategory C (ComplexShape.up ‚Ñ§)} (f : X ‚ü∂ Y) :
    IsIso (Qh.map f) ‚Üî HomotopyCategory.quasiIso C _ f := by
  constructor
  ¬∑ intro hf
    rw [HomotopyCategory.mem_quasiIso_iff]
    intro n
    rw [‚Üê NatIso.isIso_map_iff (homologyFunctorFactorsh C n) f]
    dsimp
    infer_instance
  ¬∑ intro hf
    exact Localization.inverts Qh (HomotopyCategory.quasiIso _ _) _ hf

lemma isIso_Q_map_iff_quasiIso {K L : CochainComplex C ‚Ñ§} (œÜ : K ‚ü∂ L) :
    IsIso (Q.map œÜ) ‚Üî QuasiIso œÜ := by
  apply HomologicalComplexUpToQuasiIso.isIso_Q_map_iff_mem_quasiIso

lemma isIso_Q_map_iff {K L : CochainComplex C ‚Ñ§} (œÜ : K ‚ü∂ L) :
    IsIso (Q.map œÜ) ‚Üî
      ‚àÄ (n : ‚Ñ§), IsIso ((HomologicalComplex.homologyFunctor C _ n).map œÜ) := by
  simp only [isIso_Q_map_iff_quasiIso, quasiIso_iff,
    quasiIsoAt_iff_isIso_homologyMap]
  rfl

lemma isIso_Q_map_iff' {K L : CochainComplex C ‚Ñ§} (œÜ : K ‚ü∂ L) :
    IsIso (Q.map œÜ) ‚Üî HomologicalComplex.quasiIso _ _ œÜ := by
  rw [isIso_Q_map_iff_quasiIso]
  rfl

instance {K L : CochainComplex C ‚Ñ§} (œÜ : K ‚ü∂ L) [QuasiIso œÜ] : IsIso (Q.map œÜ) := by
  simpa only [isIso_Q_map_iff_quasiIso]

lemma isIso_iff {K L : DerivedCategory C} (f : K ‚ü∂ L) :
    IsIso f ‚Üî ‚àÄ (n : ‚Ñ§), IsIso ((homologyFunctor C n).map f) := by
  constructor
  ¬∑ intro hf n
    infer_instance
  ¬∑ intro hf
    let g := (Functor.mapArrow Qh).objPreimage (Arrow.mk f)
    refine' ((MorphismProperty.RespectsIso.isomorphisms (DerivedCategory C)).arrow_iso_iff
      ((Functor.mapArrow Qh).objObjPreimageIso (Arrow.mk f))).1 _
    change IsIso (Qh.map g.hom)
    rw [isIso_Qh_map_iff, HomotopyCategory.mem_quasiIso_iff]
    intro n
    have e : Arrow.mk ((homologyFunctor C n).map f) ‚âÖ
        Arrow.mk ((HomotopyCategory.homologyFunctor _ _ n).map g.hom) :=
      ((homologyFunctor C n).mapArrow.mapIso
        (((Functor.mapArrow Qh).objObjPreimageIso (Arrow.mk f)).symm)) ‚â™‚â´
        ((Functor.mapArrowFunctor _ _).mapIso (homologyFunctorFactorsh C n)).app (Arrow.mk g.hom)
    exact ((MorphismProperty.RespectsIso.isomorphisms C).arrow_iso_iff e).1 (hf n)

lemma isZero_iff (K : DerivedCategory C) :
    IsZero K ‚Üî ‚àÄ (n : ‚Ñ§), IsZero ((homologyFunctor C n).obj K) := by
  constructor
  ¬∑ intro hK n
    rw [IsZero.iff_id_eq_zero, ‚Üê ((homologyFunctor C n).map_id K),
      (IsZero.iff_id_eq_zero K).1 hK, Functor.map_zero]
  ¬∑ intro hK
    have : IsIso (0 : K ‚ü∂ 0) := by
      rw [isIso_iff]
      intro n
      refine' ‚ü®0, _, _‚ü©
      ¬∑ apply (hK n).eq_of_src
      ¬∑ rw [zero_comp, ‚Üê (homologyFunctor C n).map_id, id_zero,
          Functor.map_zero]
    exact IsZero.of_iso (isZero_zero _) (asIso (0 : K ‚ü∂ 0))

section

variable {S : ShortComplex (CochainComplex C ‚Ñ§)} (hS : S.ShortExact)

lemma isIso_Q_map_fromOfShortComplex :
    IsIso (Q.map (CochainComplex.mappingCone.fromOfShortComplex S)) := by
  rw [isIso_Q_map_iff]
  exact CochainComplex.mappingCone.isIso_homologyMap_fromOfShortComplex hS

noncomputable def triangleOfSESŒ¥ :
  Q.obj (S.X‚ÇÉ) ‚ü∂ (Q.obj S.X‚ÇÅ)‚ü¶(1 : ‚Ñ§)‚üß :=
    have := isIso_Q_map_fromOfShortComplex hS
    inv (Q.map (CochainComplex.mappingCone.fromOfShortComplex S)) ‚â´
      Q.map (CochainComplex.mappingCone.triangle S.f).mor‚ÇÉ ‚â´
      (Q.commShiftIso (1 : ‚Ñ§)).hom.app S.X‚ÇÅ

noncomputable def triangleOfSES : Triangle (DerivedCategory C) :=
  Triangle.mk (Q.map S.f) (Q.map S.g) (triangleOfSESŒ¥ hS)

noncomputable def triangleOfSESIso :
    Q.mapTriangle.obj (CochainComplex.mappingCone.triangle S.f) ‚âÖ triangleOfSES hS := by
  have := isIso_Q_map_fromOfShortComplex hS
  refine' Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _)
    (asIso (Q.map (CochainComplex.mappingCone.fromOfShortComplex S))) _ _ _
  ¬∑ dsimp [triangleOfSES]
    simp only [comp_id, id_comp]
  ¬∑ dsimp [triangleOfSES, CochainComplex.mappingCone.fromOfShortComplex, asIso]
    rw [id_comp, ‚Üê Q.map_comp, CochainComplex.mappingCone.inr_desc]
  ¬∑ dsimp [triangleOfSES, triangleOfSESŒ¥]
    rw [CategoryTheory.Functor.map_id, comp_id, IsIso.hom_inv_id_assoc]

lemma triangleOfSES_distinguished :
    triangleOfSES hS ‚àà distTriang (DerivedCategory C) := by
  rw [mem_distTriang_iff]
  exact ‚ü®_, _, S.f, ‚ü®(triangleOfSESIso hS).symm‚ü©‚ü©

namespace HomologySequence

variable (T : Triangle (DerivedCategory C)) (hT : T ‚àà distTriang _)
  (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (h : n‚ÇÄ + 1 = n‚ÇÅ)

noncomputable def Œ¥ : (homologyFunctor C n‚ÇÄ).obj T.obj‚ÇÉ ‚ü∂ (homologyFunctor C n‚ÇÅ).obj T.obj‚ÇÅ :=
  (homologyFunctor C 0).shiftMap T.mor‚ÇÉ n‚ÇÄ n‚ÇÅ (by rw [add_comm 1, h])

lemma comp_Œ¥ : (homologyFunctor C n‚ÇÄ).map T.mor‚ÇÇ ‚â´ Œ¥ T n‚ÇÄ n‚ÇÅ h = 0 :=
  (homologyFunctor C 0).comp_homologySequenceŒ¥ _ hT _ _ h

lemma Œ¥_comp : Œ¥ T n‚ÇÄ n‚ÇÅ h ‚â´ (homologyFunctor C n‚ÇÅ).map T.mor‚ÇÅ = 0 :=
  (homologyFunctor C 0).homologySequenceŒ¥_comp _ hT _ _ h

lemma exact‚ÇÇ :
  (ShortComplex.mk ((homologyFunctor C n‚ÇÄ).map T.mor‚ÇÅ) ((homologyFunctor C n‚ÇÄ).map T.mor‚ÇÇ)
    (by simp only [‚Üê Functor.map_comp, comp_distTriang_mor_zero‚ÇÅ‚ÇÇ _ hT,
      Functor.map_zero])).Exact :=
  (homologyFunctor C 0).homologySequence_exact‚ÇÇ _ hT _

lemma exact‚ÇÉ :
  (ShortComplex.mk _ _ (comp_Œ¥ T hT n‚ÇÄ n‚ÇÅ h)).Exact :=
  (homologyFunctor C 0).homologySequence_exact‚ÇÉ _ hT _ _ h

lemma exact‚ÇÅ :
  (ShortComplex.mk _ _ (Œ¥_comp T hT n‚ÇÄ n‚ÇÅ h)).Exact :=
  (homologyFunctor C 0).homologySequence_exact‚ÇÅ _ hT _ _ h

lemma epi_homologyMap_mor‚ÇÅ_iff :
    Epi ((homologyFunctor C n‚ÇÄ).map T.mor‚ÇÅ) ‚Üî (homologyFunctor C n‚ÇÄ).map T.mor‚ÇÇ = 0 :=
  (homologyFunctor C 0).homologySequence_epi_shift_map_mor‚ÇÅ_iff _ hT _

lemma mono_homologyMap_mor‚ÇÅ_iff :
    Mono ((homologyFunctor C n‚ÇÅ).map T.mor‚ÇÅ) ‚Üî Œ¥ T n‚ÇÄ n‚ÇÅ h  = 0 :=
  (homologyFunctor C 0).homologySequence_mono_shift_map_mor‚ÇÅ_iff _ hT _ _ h

lemma isIso_homologyMap_mor‚ÇÅ_iff :
    IsIso ((homologyFunctor C n‚ÇÅ).map T.mor‚ÇÅ) ‚Üî
      Œ¥ T n‚ÇÄ n‚ÇÅ h  = 0 ‚àß (homologyFunctor C n‚ÇÅ).map T.mor‚ÇÇ = 0 :=
  (homologyFunctor C 0).homologySequence_isIso_shift_map_mor‚ÇÅ_iff _ hT _ _ h

lemma isIso_homologyMap_mor‚ÇÇ_iff :
    IsIso ((homologyFunctor C n‚ÇÄ).map T.mor‚ÇÇ) ‚Üî
      Œ¥ T n‚ÇÄ n‚ÇÅ h  = 0 ‚àß (homologyFunctor C n‚ÇÄ).map T.mor‚ÇÅ = 0 :=
  (homologyFunctor C 0).homologySequence_isIso_shift_map_mor‚ÇÇ_iff _ hT _ _ h

end HomologySequence

end

lemma right_fac (X Y : CochainComplex C ‚Ñ§) (f : Q.obj X ‚ü∂ Q.obj Y) :
    ‚àÉ (X' : CochainComplex C ‚Ñ§) (s : X' ‚ü∂ X) (hs : IsIso (Q.map s)) (g : X' ‚ü∂ Y),
      f = inv (Q.map s) ‚â´ Q.map g := by
  have ‚ü®œÜ, hœÜ‚ü© := Localization.exists_rightFraction Qh (HomotopyCategory.quasiIso C _) f
  obtain ‚ü®X', s, hs, g, rfl‚ü© := œÜ.cases
  obtain ‚ü®X', rfl‚ü© := HomotopyCategory.quotient_obj_surjective X'
  obtain ‚ü®s, rfl‚ü© := (HomotopyCategory.quotient _ _).map_surjective s
  obtain ‚ü®g, rfl‚ü© := (HomotopyCategory.quotient _ _).map_surjective g
  rw [‚Üê isIso_Qh_map_iff] at hs
  exact ‚ü®X', s, hs, g, hœÜ‚ü©

lemma left_fac (X Y : CochainComplex C ‚Ñ§) (f : Q.obj X ‚ü∂ Q.obj Y) :
    ‚àÉ (Y' : CochainComplex C ‚Ñ§) (g : X ‚ü∂ Y') (s : Y ‚ü∂ Y') (hs : IsIso (Q.map s)),
      f = Q.map g ‚â´ inv (Q.map s) := by
  have ‚ü®œÜ, hœÜ‚ü© := Localization.exists_leftFraction Qh (HomotopyCategory.quasiIso C _) f
  obtain ‚ü®X', g, s, hs, rfl‚ü© := œÜ.cases
  obtain ‚ü®X', rfl‚ü© := HomotopyCategory.quotient_obj_surjective X'
  obtain ‚ü®s, rfl‚ü© := (HomotopyCategory.quotient _ _).map_surjective s
  obtain ‚ü®g, rfl‚ü© := (HomotopyCategory.quotient _ _).map_surjective g
  rw [‚Üê isIso_Qh_map_iff] at hs
  exact ‚ü®X', g, s, hs, hœÜ‚ü©

end DerivedCategory
