import Mathlib.Algebra.Homology.DerivedCategory.Basic

open CategoryTheory Category Limits Pretriangulated Limits ZeroObject

variable {C : Type _} [Category C]

section preadditive

variable [Preadditive C]

namespace CochainComplex

open HomologicalComplex

variable (K L : CochainComplex C ‚Ñ§) (e : K ‚âÖ L) (n : ‚Ñ§)

class IsStrictlyLE : Prop where
  isZero (i : ‚Ñ§) (hi : n < i) : IsZero (K.X i)

lemma isZero_of_isStrictlyLE (n i : ‚Ñ§) (hi : n < i) [K.IsStrictlyLE n] :
    IsZero (K.X i) := IsStrictlyLE.isZero i hi

class IsStrictlyGE : Prop where
  isZero (i : ‚Ñ§) (hi : i < n) : IsZero (K.X i)

lemma isZero_of_isStrictlyGE (i : ‚Ñ§) (hi : i < n) [K.IsStrictlyGE n] :
    IsZero (K.X i) := IsStrictlyGE.isZero i hi

lemma isStrictlyLE_of_LE (p q : ‚Ñ§) (hpq : p ‚â§ q) [K.IsStrictlyLE p] :
  K.IsStrictlyLE q := ‚ü®fun i hi => K.isZero_of_isStrictlyLE p i (by linarith)‚ü©

lemma isStrictlyGE_of_GE (p q : ‚Ñ§) (hpq : p ‚â§ q) [K.IsStrictlyGE q] :
  K.IsStrictlyGE p := ‚ü®fun i hi => K.isZero_of_isStrictlyGE q i (by linarith)‚ü©

lemma isStrictlyLE_shift [K.IsStrictlyLE n] (a n' : ‚Ñ§) (h : a + n' = n) :
    (K‚ü¶a‚üß).IsStrictlyLE n' := ‚ü®fun i hi =>
  IsZero.of_iso (K.isZero_of_isStrictlyLE n _ (by linarith)) (K.shiftFunctorObjXIso a i _ rfl)‚ü©

lemma isStrictlyGE_shift [K.IsStrictlyGE n] (a n' : ‚Ñ§) (h : a + n' = n) :
    (K‚ü¶a‚üß).IsStrictlyGE n' := ‚ü®fun i hi =>
  IsZero.of_iso (K.isZero_of_isStrictlyGE n _ (by linarith)) (K.shiftFunctorObjXIso a i _ rfl)‚ü©

variable {K L}

lemma isStrictlyLE_of_iso [K.IsStrictlyLE n] : L.IsStrictlyLE n := ‚ü®fun i hi =>
  IsZero.of_iso (K.isZero_of_isStrictlyLE n i hi)
    ((eval _ _ i).mapIso e.symm)‚ü©

lemma isStrictlyGE_of_iso [K.IsStrictlyGE n] : L.IsStrictlyGE n := ‚ü®fun i hi =>
  IsZero.of_iso (K.isZero_of_isStrictlyGE n i hi)
    ((eval _ _ i).mapIso e.symm)‚ü©

variable(K)

lemma exists_iso_single [HasZeroObject C] (n : ‚Ñ§) [K.IsStrictlyGE n] [K.IsStrictlyLE n] :
    ‚àÉ (M : C), Nonempty (K ‚âÖ (single _ _ n).obj M) := by
  refine' ‚ü®K.X n, ‚ü®_‚ü©‚ü©
  refine' HomologicalComplex.Hom.isoOfComponents _ _
  . intro i
    by_cases i = n
    . subst h
      exact (singleObjXSelf _ _ _ _).symm
    . refine' IsZero.isoZero _ ‚â™‚â´ (IsZero.isoZero _).symm
      . by_cases hi' : i ‚â§ n
        . refine' K.isZero_of_isStrictlyGE n i _
          cases hi'.lt_or_eq <;> tauto
        . exact K.isZero_of_isStrictlyLE n i (by linarith)
      . dsimp
        rw [if_neg h]
        exact isZero_zero _
  . intro i j (hij : i + 1 = j)
    simp only [single_obj_d, comp_zero]
    by_cases i < n
    . apply (K.isZero_of_isStrictlyGE n i h).eq_of_src
    . apply IsZero.eq_of_tgt
      dsimp
      rw [if_neg]
      . exact isZero_zero _
      . linarith

instance [HasZeroObject C] (A : C) (n : ‚Ñ§) :
    IsStrictlyLE ((single C (ComplexShape.up ‚Ñ§) n).obj A) n := ‚ü®fun i hi => by
    dsimp
    rw [if_neg (by linarith)]
    exact isZero_zero _‚ü©

instance [HasZeroObject C] (A : C) (n : ‚Ñ§) :
    IsStrictlyLE ((singleFunctor C n).obj A) n :=
  (inferInstance : IsStrictlyLE ((single C (ComplexShape.up ‚Ñ§) n).obj A) n)

instance [HasZeroObject C] (A : C) (n : ‚Ñ§) :
    IsStrictlyGE ((single C (ComplexShape.up ‚Ñ§) n).obj A) n := ‚ü®fun i hi => by
    dsimp
    rw [if_neg (by linarith)]
    exact isZero_zero _‚ü©

instance [HasZeroObject C] (A : C) (n : ‚Ñ§) :
    IsStrictlyGE ((singleFunctor C n).obj A) n :=
  (inferInstance : IsStrictlyGE ((single C (ComplexShape.up ‚Ñ§) n).obj A) n)

instance [HasZeroObject C] (n : ‚Ñ§) : IsStrictlyGE (0 : CochainComplex C ‚Ñ§) n :=
  ‚ü®fun i _ => by
    rw [IsZero.iff_id_eq_zero]
    change (eval _ _ i).map (ùüô (0 : CochainComplex C ‚Ñ§)) = 0
    simp only [id_zero, eval_map, zero_f]‚ü©

instance [HasZeroObject C] (n : ‚Ñ§) : IsStrictlyLE (0 : CochainComplex C ‚Ñ§) n :=
  ‚ü®fun i _ => by
    rw [IsZero.iff_id_eq_zero]
    change (eval _ _ i).map (ùüô (0 : CochainComplex C ‚Ñ§)) = 0
    simp only [id_zero, eval_map, zero_f]‚ü©

section

variable {D : Type*} [Category D] [Preadditive D] (F : C ‚•§ D) [F.Additive]

instance (K : CochainComplex C ‚Ñ§) (n : ‚Ñ§) [K.IsStrictlyLE n] :
  IsStrictlyLE ((F.mapHomologicalComplex _).obj K) n := ‚ü®fun i hi => by
    have := (K.isZero_of_isStrictlyLE n i hi)
    simp only [IsZero.iff_id_eq_zero] at this ‚ä¢
    change ùüô (F.obj (K.X i)) = 0
    rw [‚Üê F.map_id, this, F.map_zero]‚ü©

instance (K : CochainComplex C ‚Ñ§) (n : ‚Ñ§) [K.IsStrictlyGE n] :
  IsStrictlyGE ((F.mapHomologicalComplex _).obj K) n := ‚ü®fun i hi => by
    have := (K.isZero_of_isStrictlyGE n i hi)
    simp only [IsZero.iff_id_eq_zero] at this ‚ä¢
    change ùüô (F.obj (K.X i)) = 0
    rw [‚Üê F.map_id, this, F.map_zero]‚ü©

end

end CochainComplex

end preadditive

variable [Abelian C]

namespace CochainComplex

open HomologicalComplex

variable (K L : CochainComplex C ‚Ñ§) (e : K ‚âÖ L) (n : ‚Ñ§)

class IsLE : Prop where
  isZero (i : ‚Ñ§) (hi : n < i) : IsZero (K.homology i)

lemma isZero_of_isLE (i : ‚Ñ§) (hi : n < i) [K.IsLE n] :
    IsZero (K.homology i) := IsLE.isZero i hi

class IsGE : Prop where
  isZero (i : ‚Ñ§) (hi : i < n) : IsZero (K.homology i)

lemma isZero_of_isGE (i : ‚Ñ§) (hi : i < n) [K.IsGE n] :
    IsZero (K.homology i) := IsGE.isZero i hi

instance (K : CochainComplex C ‚Ñ§) [K.IsStrictlyLE n] : K.IsLE n := ‚ü®fun i hi =>
  K.isZero_homology_of_isZero _ (K.isZero_of_isStrictlyLE n i hi)‚ü©

instance (K : CochainComplex C ‚Ñ§) [K.IsStrictlyGE n] : K.IsGE n := ‚ü®fun i hi =>
  K.isZero_homology_of_isZero _ (K.isZero_of_isStrictlyGE n i hi)‚ü©

lemma isLE_of_LE (p q : ‚Ñ§) (hpq : p ‚â§ q) [K.IsLE p] :
  K.IsLE q := ‚ü®fun i hi => K.isZero_of_isLE p i (by linarith)‚ü©

lemma isGE_of_GE (p q : ‚Ñ§) (hpq : p ‚â§ q) [K.IsGE q] :
  K.IsGE p := ‚ü®fun i hi => K.isZero_of_isGE q i (by linarith)‚ü©

variable {K L}

lemma isLE_of_iso [K.IsLE n] : L.IsLE n := ‚ü®fun i hi =>
  IsZero.of_iso (K.isZero_of_isLE n i hi)
    ((homologyFunctor _ _ i).mapIso e.symm)‚ü©

lemma isGE_of_iso [K.IsGE n] : L.IsGE n := ‚ü®fun i hi =>
  IsZero.of_iso (K.isZero_of_isGE n i hi)
    ((homologyFunctor _ _ i).mapIso e.symm)‚ü©

variable (K)

lemma isLE_shift [K.IsLE n] (a n' : ‚Ñ§) (h : a + n' = n) : (K‚ü¶a‚üß).IsLE n' := ‚ü®fun i hi =>
  IsZero.of_iso (K.isZero_of_isLE n (a+i) (by linarith))
    (((homologyFunctor C _ (0 : ‚Ñ§)).shiftIso a i _ rfl).app K)‚ü©

lemma isGE_shift [K.IsGE n] (a n' : ‚Ñ§) (h : a + n' = n) : (K‚ü¶a‚üß).IsGE n' := ‚ü®fun i hi =>
  IsZero.of_iso (K.isZero_of_isGE n (a+i) (by linarith))
    (((homologyFunctor C _ (0 : ‚Ñ§)).shiftIso a i _ rfl).app K)‚ü©

end CochainComplex

namespace DerivedCategory

variable [HasDerivedCategory C]
variable (K L : DerivedCategory C) (e : K ‚âÖ L) (n : ‚Ñ§)

class IsLE : Prop where
  isZero (i : ‚Ñ§) (hi : n < i) : IsZero ((homologyFunctor _ i).obj K)

lemma isZero_of_isLE (i : ‚Ñ§) (hi : n < i) [K.IsLE n] :
    IsZero ((homologyFunctor _ i).obj K) := IsLE.isZero i hi

class IsGE : Prop where
  isZero (i : ‚Ñ§) (hi : i < n) : IsZero ((homologyFunctor _ i).obj K)

lemma isZero_of_isGE (i : ‚Ñ§) (hi : i < n) [K.IsGE n] :
    IsZero ((homologyFunctor _ i).obj K) := IsGE.isZero i hi

instance (K : CochainComplex C ‚Ñ§) (n : ‚Ñ§) [K.IsLE n] :
    (Q.obj K).IsLE n :=
  ‚ü®fun i hi => IsZero.of_iso (K.isZero_of_isLE n i hi) ((homologyFunctorFactors C i).app K)‚ü©

instance (K : CochainComplex C ‚Ñ§) (n : ‚Ñ§) [K.IsGE n] :
    (Q.obj K).IsGE n :=
  ‚ü®fun i hi => IsZero.of_iso (K.isZero_of_isGE n i hi) ((homologyFunctorFactors C i).app K)‚ü©

lemma isLE_Q_obj_iff (K : CochainComplex C ‚Ñ§) (n : ‚Ñ§) :
    (Q.obj K).IsLE n ‚Üî K.IsLE n := by
  constructor
  . intro
    exact ‚ü®fun i hi => IsZero.of_iso (isZero_of_isLE _ n i hi)
      ((homologyFunctorFactors C i).app K).symm‚ü©
  . intro
    infer_instance

lemma isGE_Q_obj_iff (K : CochainComplex C ‚Ñ§) (n : ‚Ñ§) :
    (Q.obj K).IsGE n ‚Üî K.IsGE n := by
  constructor
  . intro
    exact ‚ü®fun i hi => IsZero.of_iso (isZero_of_isGE _ n i hi)
      ((homologyFunctorFactors C i).app K).symm‚ü©
  . intro
    infer_instance

lemma isLE_shift [K.IsLE n] (a n' : ‚Ñ§) (h : a + n' = n) : (K‚ü¶a‚üß).IsLE n' := ‚ü®fun i hi =>
  IsZero.of_iso (K.isZero_of_isLE n (a+i) (by linarith))
    (((homologyFunctor C (0 : ‚Ñ§)).shiftIso a i _ rfl).app K)‚ü©

lemma isGE_shift [K.IsGE n] (a n' : ‚Ñ§) (h : a + n' = n) : (K‚ü¶a‚üß).IsGE n' := ‚ü®fun i hi =>
  IsZero.of_iso (K.isZero_of_isGE n (a+i) (by linarith))
    (((homologyFunctor C (0 : ‚Ñ§)).shiftIso a i _ rfl).app K)‚ü©

lemma isLE_of_LE (p q : ‚Ñ§) (hpq : p ‚â§ q) [K.IsLE p] :
  K.IsLE q := ‚ü®fun i hi => K.isZero_of_isLE p i (by linarith)‚ü©

lemma isGE_of_GE (p q : ‚Ñ§) (hpq : p ‚â§ q) [K.IsGE q] :
  K.IsGE p := ‚ü®fun i hi => K.isZero_of_isGE q i (by linarith)‚ü©

variable {K L}

lemma isLE_of_iso [K.IsLE n] : L.IsLE n := ‚ü®fun i hi =>
  IsZero.of_iso (K.isZero_of_isLE n i hi)
    ((homologyFunctor _ i).mapIso e.symm)‚ü©

lemma isGE_of_iso [K.IsGE n] : L.IsGE n := ‚ü®fun i hi =>
  IsZero.of_iso (K.isZero_of_isGE n i hi)
    ((homologyFunctor _ i).mapIso e.symm)‚ü©

lemma distTriang‚ÇÉ_isGE_iff (T : Triangle (DerivedCategory C)) (hT : T ‚àà distTriang _)
    (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (h : n‚ÇÄ + 1 = n‚ÇÅ) :
    T.obj‚ÇÉ.IsGE n‚ÇÅ ‚Üî (‚àÄ (i : ‚Ñ§) (_ : i ‚â§ n‚ÇÄ), IsIso ((homologyFunctor C i).map T.mor‚ÇÅ)) ‚àß
      (Mono ((homologyFunctor C n‚ÇÅ).map T.mor‚ÇÅ)) := by
  simp only [HomologySequence.mono_homologyMap_mor‚ÇÅ_iff _ hT n‚ÇÄ n‚ÇÅ h]
  constructor
  . simp only [fun (i : ‚Ñ§) =>
      HomologySequence.isIso_homologyMap_mor‚ÇÅ_iff _ hT (i-1) i (by linarith)]
    intro H
    constructor
    . intro i hi
      constructor
      . apply IsZero.eq_of_src
        exact T.obj‚ÇÉ.isZero_of_isGE n‚ÇÅ _ (by linarith)
      . apply IsZero.eq_of_tgt
        exact T.obj‚ÇÉ.isZero_of_isGE n‚ÇÅ _ (by linarith)
    . apply IsZero.eq_of_src
      exact T.obj‚ÇÉ.isZero_of_isGE n‚ÇÅ n‚ÇÄ (by linarith)
  . subst h
    intro ‚ü®h‚ÇÄ, h‚ÇÅ‚ü©
    constructor
    intro i hi
    apply (HomologySequence.exact‚ÇÉ _ hT i (i+1) rfl).isZero_of_both_zeros
    . dsimp
      have h' := h‚ÇÄ i (by linarith)
      rw [HomologySequence.isIso_homologyMap_mor‚ÇÅ_iff _ hT (i-1) i (by linarith)] at h'
      exact h'.2
    . dsimp
      by_cases i < n‚ÇÄ
      . have h' := h‚ÇÄ (i+1) (by linarith)
        rw [HomologySequence.isIso_homologyMap_mor‚ÇÅ_iff _ hT i (i+1) (by linarith)] at h'
        exact h'.1
      . obtain rfl : n‚ÇÄ = i := by linarith
        exact h‚ÇÅ

instance (A : C) (n : ‚Ñ§) : IsLE ((singleFunctor C n).obj A) n := by
  have e : (singleFunctor C n).obj A ‚âÖ Q.obj ((CochainComplex.singleFunctor C n).obj A) :=
    ((SingleFunctors.evaluation _ _ n).mapIso (singleFunctorsPostCompQIso C)).app A
  exact isLE_of_iso e.symm n

instance (A : C) (n : ‚Ñ§) : IsGE ((singleFunctor C n).obj A) n := by
  have e : (singleFunctor C n).obj A ‚âÖ Q.obj ((CochainComplex.singleFunctor C n).obj A) :=
    ((SingleFunctors.evaluation _ _ n).mapIso (singleFunctorsPostCompQIso C)).app A
  exact isGE_of_iso e.symm n

end DerivedCategory
