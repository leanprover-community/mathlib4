/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Homology.DerivedCategory.SingleTriangle
import Mathlib.CategoryTheory.Localization.SmallShiftedHom

/-!
# Ext groups in abelian categories

Let `C` be an abelian category (with `C : Type u` and `Category.{v} C`).
In this file, we introduce the assumption `HasExt.{w} C` which asserts
that morphisms between single complexes in arbitrary degrees in
the derived category of `C` are `w`-small. Under this assumption,
we define `Ext.{w} X Y n : Type w` as shrunk versions of suitable
types of morphisms in the derived category. In particular, when `C` has
enough projectives or enough injectives, the property `HasExt.{v} C`
shall hold (TODO).

Note: in certain situations, `w := v` shall be the preferred
choice of universe (e.g. if `C := ModuleCat.{v} R` with `R : Type v`).
However, in the development of the API for Ext-groups, it is important
to keep a larger degree of generality for universes, as `w < v`
may happen in certain situations. Indeed, if `X : Scheme.{u}`,
then the underlying category of the √©tale site of `X` shall be a large
category. However, the category `Sheaf X.Etale AddCommGroupCat.{u}`
shall have good properties (because there is a small category of affine
schemes with the same category of sheaves), and even though the type of
morphisms in `Sheaf X.Etale AddCommGroupCat.{u}` shall be
in `Type (u + 1)`, these types are going to be `u`-small.
Then, for `C := Sheaf X.etale AddCommGroupCat.{u}`, we will have
`Category.{u + 1} C`, but `HasExt.{u} C` will hold
(as `C` has enough injectives). Then, the `Ext` groups between √©tale
sheaves over `X` shall be in `Type u`.

## TODO
* compute `Ext X Y 0`
* define the class in `Ext S.X‚ÇÉ S.X‚ÇÅ 1` of a short exact short complex `S`
* construct the long exact sequences of `Ext`.

-/

universe w' w v u

namespace CategoryTheory

variable (C : Type u) [Category.{v} C] [Abelian C]

open Localization Limits ZeroObject DerivedCategory Pretriangulated

/-- The property that morphisms between single complexes in arbitrary degrees are `w`-small
in the derived category. -/
abbrev HasExt : Prop :=
  ‚àÄ (X Y : C), HasSmallLocalizedShiftedHom.{w} (HomologicalComplex.quasiIso C (ComplexShape.up ‚Ñ§)) ‚Ñ§
    ((CochainComplex.singleFunctor C 0).obj X) ((CochainComplex.singleFunctor C 0).obj Y)

-- TODO: when the canonical t-structure is formalized, replace `n : ‚Ñ§` by `n : ‚Ñï`
lemma hasExt_iff [HasDerivedCategory.{w'} C] :
    HasExt.{w} C ‚Üî ‚àÄ (X Y : C) (n : ‚Ñ§), Small.{w}
      ((singleFunctor C 0).obj X ‚ü∂
        (((singleFunctor C 0).obj Y)‚ü¶n‚üß)) := by
  dsimp [HasExt]
  simp only [hasSmallLocalizedShiftedHom_iff _ _ Q]
  constructor
  ¬∑ intro h X Y n
    exact (small_congr ((shiftFunctorZero _ ‚Ñ§).app
      ((singleFunctor C 0).obj X)).homFromEquiv).1 (h X Y 0 n)
  ¬∑ intro h X Y a b
    refine (small_congr ?_).1 (h X Y (b - a))
    exact (Functor.FullyFaithful.ofFullyFaithful
      (shiftFunctor _ a)).homEquiv.trans
      ((shiftFunctorAdd' _ _ _ _ (Int.sub_add_cancel b a)).symm.app _).homToEquiv

lemma hasExt_of_hasDerivedCategory [HasDerivedCategory.{w} C] : HasExt.{w} C := by
  rw [hasExt_iff.{w}]
  infer_instance

variable {C}

variable [HasExt.{w} C]

namespace Abelian

/-- A Ext-group in an abelian category `C`, defined as a `Type w` when `[HasExt.{w} C]`. -/
def Ext (X Y : C) (n : ‚Ñï) : Type w :=
  SmallShiftedHom.{w} (HomologicalComplex.quasiIso C (ComplexShape.up ‚Ñ§))
    ((CochainComplex.singleFunctor C 0).obj X)
    ((CochainComplex.singleFunctor C 0).obj Y) (n : ‚Ñ§)

namespace Ext

variable {X Y Z T : C}

/-- The composition of `Ext`. -/
noncomputable def comp {a b : ‚Ñï} (Œ± : Ext X Y a) (Œ≤ : Ext Y Z b) {c : ‚Ñï} (h : a + b = c) :
    Ext X Z c :=
  SmallShiftedHom.comp Œ± Œ≤ (by omega)

lemma comp_assoc {a‚ÇÅ a‚ÇÇ a‚ÇÉ a‚ÇÅ‚ÇÇ a‚ÇÇ‚ÇÉ a : ‚Ñï} (Œ± : Ext X Y a‚ÇÅ) (Œ≤ : Ext Y Z a‚ÇÇ) (Œ≥ : Ext Z T a‚ÇÉ)
    (h‚ÇÅ‚ÇÇ : a‚ÇÅ + a‚ÇÇ = a‚ÇÅ‚ÇÇ) (h‚ÇÇ‚ÇÉ : a‚ÇÇ + a‚ÇÉ = a‚ÇÇ‚ÇÉ) (h : a‚ÇÅ + a‚ÇÇ + a‚ÇÉ = a) :
    (Œ±.comp Œ≤ h‚ÇÅ‚ÇÇ).comp Œ≥ (show a‚ÇÅ‚ÇÇ + a‚ÇÉ = a by omega) =
      Œ±.comp (Œ≤.comp Œ≥ h‚ÇÇ‚ÇÉ) (by omega) :=
  SmallShiftedHom.comp_assoc _ _ _ _ _ _ (by omega)

@[simp]
lemma comp_assoc_of_second_deg_zero
    {a‚ÇÅ a‚ÇÉ a‚ÇÅ‚ÇÉ : ‚Ñï} (Œ± : Ext X Y a‚ÇÅ) (Œ≤ : Ext Y Z 0) (Œ≥ : Ext Z T a‚ÇÉ)
    (h‚ÇÅ‚ÇÉ : a‚ÇÅ + a‚ÇÉ = a‚ÇÅ‚ÇÉ) :
    (Œ±.comp Œ≤ (add_zero _)).comp Œ≥ h‚ÇÅ‚ÇÉ = Œ±.comp (Œ≤.comp Œ≥ (zero_add _)) h‚ÇÅ‚ÇÉ := by
  apply comp_assoc
  omega

@[simp]
lemma comp_assoc_of_third_deg_zero
    {a‚ÇÅ a‚ÇÇ a‚ÇÅ‚ÇÇ : ‚Ñï} (Œ± : Ext X Y a‚ÇÅ) (Œ≤ : Ext Y Z a‚ÇÇ) (Œ≥ : Ext Z T 0)
    (h‚ÇÅ‚ÇÇ : a‚ÇÅ + a‚ÇÇ = a‚ÇÅ‚ÇÇ) :
    (Œ±.comp Œ≤ h‚ÇÅ‚ÇÇ).comp Œ≥ (add_zero _) = Œ±.comp (Œ≤.comp Œ≥ (add_zero _)) h‚ÇÅ‚ÇÇ := by
  apply comp_assoc
  omega

section

variable [HasDerivedCategory.{w'} C]

/-- When an instance of `[HasDerivedCategory.{w'} C]` is available, this is the bijection
between `Ext.{w} X Y n` and a type of morphisms in the derived category. -/
noncomputable def homEquiv {n : ‚Ñï} :
    Ext.{w} X Y n ‚âÉ ShiftedHom ((singleFunctor C 0).obj X)
      ((singleFunctor C 0).obj Y) (n : ‚Ñ§) :=
  SmallShiftedHom.equiv (HomologicalComplex.quasiIso C (ComplexShape.up ‚Ñ§)) Q

/-- The morphism in the derived category which corresponds to an element in `Ext X Y a`. -/
noncomputable abbrev hom {a : ‚Ñï} (Œ± : Ext X Y a) :
    ShiftedHom ((singleFunctor C 0).obj X) ((singleFunctor C 0).obj Y) (a : ‚Ñ§) :=
  homEquiv Œ±

@[simp]
lemma comp_hom {a b : ‚Ñï} (Œ± : Ext X Y a) (Œ≤ : Ext Y Z b) {c : ‚Ñï} (h : a + b = c) :
    (Œ±.comp Œ≤ h).hom = Œ±.hom.comp Œ≤.hom (by omega) := by
  apply SmallShiftedHom.equiv_comp

@[ext]
lemma ext {n : ‚Ñï} {Œ± Œ≤ : Ext X Y n} (h : Œ±.hom = Œ≤.hom) : Œ± = Œ≤ :=
  homEquiv.injective h

lemma ext_iff {n : ‚Ñï} {Œ± Œ≤ : Ext X Y n} : Œ± = Œ≤ ‚Üî Œ±.hom = Œ≤.hom :=
  ‚ü®fun h ‚Ü¶ by rw [h], ext‚ü©

end

/-- The canonical map `(X ‚ü∂ Y) ‚Üí Ext X Y 0`. -/
noncomputable def mk‚ÇÄ (f : X ‚ü∂ Y) : Ext X Y 0 := SmallShiftedHom.mk‚ÇÄ _ _ (by simp)
  ((CochainComplex.singleFunctor C 0).map f)

@[simp]
lemma mk‚ÇÄ_hom [HasDerivedCategory.{w'} C] (f : X ‚ü∂ Y) :
    (mk‚ÇÄ f).hom = ShiftedHom.mk‚ÇÄ _ (by simp) ((singleFunctor C 0).map f) := by
  apply SmallShiftedHom.equiv_mk‚ÇÄ

@[simp 1100]
lemma mk‚ÇÄ_comp_mk‚ÇÄ (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (mk‚ÇÄ f).comp (mk‚ÇÄ g) (zero_add 0) = mk‚ÇÄ (f ‚â´ g) := by
  letI := HasDerivedCategory.standard C; ext; simp

@[simp 1100]
lemma mk‚ÇÄ_comp_mk‚ÇÄ_assoc (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) {n : ‚Ñï} (Œ± : Ext Z T n) :
    (mk‚ÇÄ f).comp ((mk‚ÇÄ g).comp Œ± (zero_add n)) (zero_add n) =
      (mk‚ÇÄ (f ‚â´ g)).comp Œ± (zero_add n) := by
  rw [‚Üê mk‚ÇÄ_comp_mk‚ÇÄ, comp_assoc]
  omega

variable {n : ‚Ñï}

/-! The abelian group structure on `Ext X Y n` is defined by transporting the
abelian group structure on the constructed derived category
(given by `HasDerivedCategory.standard`). This constructed derived category
is used in order to obtain most of the compatibilities satisfied by
this abelian group structure. It is then shown that the bijection
`homEquiv` between `Ext X Y n` and Hom-types in the derived category
cane be promoted to an additive equivalence for any `[HasDerivedCategory C]` instance. -/

noncomputable instance : AddCommGroup (Ext X Y n) :=
  letI := HasDerivedCategory.standard C
  homEquiv.addCommGroup

/-- The map from `Ext X Y n` to a `ShiftedHom` type in the *constructed* derived
category given by `HasDerivedCategory.standard`: this definition is introduced
only in order to prove properties of the abelian group structure on `Ext`-groups.
Do not use this definition: use the more general `hom` instead. -/
noncomputable abbrev hom' (Œ± : Ext X Y n) :
  letI := HasDerivedCategory.standard C
  ShiftedHom ((singleFunctor C 0).obj X) ((singleFunctor C 0).obj Y) (n : ‚Ñ§) :=
  letI := HasDerivedCategory.standard C
  Œ±.hom

private lemma add_hom' (Œ± Œ≤ : Ext X Y n) : (Œ± + Œ≤).hom' = Œ±.hom' + Œ≤.hom' :=
  letI := HasDerivedCategory.standard C
  homEquiv.symm.injective (Equiv.symm_apply_apply _ _)

private lemma neg_hom' (Œ± : Ext X Y n) : (-Œ±).hom' = -Œ±.hom' :=
  letI := HasDerivedCategory.standard C
  homEquiv.symm.injective (Equiv.symm_apply_apply _ _)

variable (X Y n) in
private lemma zero_hom' : (0 : Ext X Y n).hom' = 0 :=
  letI := HasDerivedCategory.standard C
  homEquiv.symm.injective (Equiv.symm_apply_apply _ _)

@[simp]
lemma add_comp (Œ±‚ÇÅ Œ±‚ÇÇ : Ext X Y n) {m : ‚Ñï} (Œ≤ : Ext Y Z m) {p : ‚Ñï} (h : n + m = p) :
    (Œ±‚ÇÅ + Œ±‚ÇÇ).comp Œ≤ h = Œ±‚ÇÅ.comp Œ≤ h + Œ±‚ÇÇ.comp Œ≤ h := by
  letI := HasDerivedCategory.standard C; ext; simp [add_hom']

@[simp]
lemma comp_add (Œ± : Ext X Y n) {m : ‚Ñï} (Œ≤‚ÇÅ Œ≤‚ÇÇ : Ext Y Z m) {p : ‚Ñï} (h : n + m = p) :
    Œ±.comp (Œ≤‚ÇÅ + Œ≤‚ÇÇ) h = Œ±.comp Œ≤‚ÇÅ h + Œ±.comp Œ≤‚ÇÇ h := by
  letI := HasDerivedCategory.standard C; ext; simp [add_hom']

@[simp]
lemma neg_comp (Œ± : Ext X Y n) {m : ‚Ñï} (Œ≤ : Ext Y Z m) {p : ‚Ñï} (h : n + m = p) :
    (-Œ±).comp Œ≤ h = -Œ±.comp Œ≤ h := by
  letI := HasDerivedCategory.standard C; ext; simp [neg_hom']

@[simp]
lemma comp_neg (Œ± : Ext X Y n) {m : ‚Ñï} (Œ≤ : Ext Y Z m) {p : ‚Ñï} (h : n + m = p) :
    Œ±.comp (-Œ≤) h = -Œ±.comp Œ≤ h := by
  letI := HasDerivedCategory.standard C; ext; simp [neg_hom']

variable (X n) in
@[simp]
lemma zero_comp {m : ‚Ñï} (Œ≤ : Ext Y Z m) (p : ‚Ñï) (h : n + m = p) :
    (0 : Ext X Y n).comp Œ≤ h = 0 := by
  letI := HasDerivedCategory.standard C; ext; simp [zero_hom']

@[simp]
lemma comp_zero (Œ± : Ext X Y n) (Z : C) (m : ‚Ñï) (p : ‚Ñï) (h : n + m = p) :
    Œ±.comp (0 : Ext Y Z m) h = 0 := by
  letI := HasDerivedCategory.standard C; ext; simp [zero_hom']

@[simp]
lemma mk‚ÇÄ_id_comp (Œ± : Ext X Y n) :
    (mk‚ÇÄ (ùüô X)).comp Œ± (zero_add n) = Œ± := by
  letI := HasDerivedCategory.standard C; ext; simp

@[simp]
lemma comp_mk‚ÇÄ_id (Œ± : Ext X Y n) :
    Œ±.comp (mk‚ÇÄ (ùüô Y)) (add_zero n) = Œ± := by
  letI := HasDerivedCategory.standard C; ext; simp

variable (X Y) in
@[simp]
lemma mk‚ÇÄ_zero : mk‚ÇÄ (0 : X ‚ü∂ Y) = 0 := by
  letI := HasDerivedCategory.standard C; ext; simp [zero_hom']

section

variable [HasDerivedCategory.{w'} C]

variable (X Y n) in
@[simp]
lemma zero_hom : (0 : Ext X Y n).hom = 0 := by
  let Œ≤ : Ext 0 Y n := 0
  have hŒ≤ : Œ≤.hom = 0 := by apply (Functor.map_isZero _ (isZero_zero C)).eq_of_src
  have : (0 : Ext X Y n) = (0 : Ext X 0 0).comp Œ≤ (zero_add n) := by simp [Œ≤]
  rw [this, comp_hom, hŒ≤, ShiftedHom.comp_zero]

attribute [local instance] preservesBinaryBiproductsOfPreservesBiproducts

lemma biprod_ext {X‚ÇÅ X‚ÇÇ : C} {Œ± Œ≤ : Ext (X‚ÇÅ ‚äû X‚ÇÇ) Y n}
    (h‚ÇÅ : (mk‚ÇÄ biprod.inl).comp Œ± (zero_add n) = (mk‚ÇÄ biprod.inl).comp Œ≤ (zero_add n))
    (h‚ÇÇ : (mk‚ÇÄ biprod.inr).comp Œ± (zero_add n) = (mk‚ÇÄ biprod.inr).comp Œ≤ (zero_add n)) :
    Œ± = Œ≤ := by
  letI := HasDerivedCategory.standard C
  rw [ext_iff] at h‚ÇÅ h‚ÇÇ ‚ä¢
  simp only [comp_hom, mk‚ÇÄ_hom, ShiftedHom.mk‚ÇÄ_comp] at h‚ÇÅ h‚ÇÇ
  apply BinaryCofan.IsColimit.hom_ext
    (isBinaryBilimitOfPreserves (singleFunctor C 0)
      (BinaryBiproduct.isBilimit X‚ÇÅ X‚ÇÇ)).isColimit
  all_goals assumption

@[simp]
lemma add_hom (Œ± Œ≤ : Ext X Y n) : (Œ± + Œ≤).hom = Œ±.hom + Œ≤.hom := by
  let Œ±' : Ext (X ‚äû X) Y n := (mk‚ÇÄ biprod.fst).comp Œ± (zero_add n)
  let Œ≤' : Ext (X ‚äû X) Y n := (mk‚ÇÄ biprod.snd).comp Œ≤ (zero_add n)
  have eq‚ÇÅ : Œ± + Œ≤ = (mk‚ÇÄ (biprod.lift (ùüô X) (ùüô X))).comp (Œ±' + Œ≤') (zero_add n) := by
    simp [Œ±', Œ≤']
  have eq‚ÇÇ : Œ±' + Œ≤' = homEquiv.symm (Œ±'.hom + Œ≤'.hom) := by
    apply biprod_ext
    all_goals ext; simp [Œ±', Œ≤', ‚Üê Functor.map_comp]
  simp only [eq‚ÇÅ, eq‚ÇÇ, comp_hom, Equiv.apply_symm_apply, ShiftedHom.comp_add]
  congr
  ¬∑ dsimp [Œ±']
    rw [comp_hom, mk‚ÇÄ_hom, mk‚ÇÄ_hom]
    dsimp
    rw [ShiftedHom.mk‚ÇÄ_comp_mk‚ÇÄ_assoc, ‚Üê Functor.map_comp,
      biprod.lift_fst, Functor.map_id, ShiftedHom.mk‚ÇÄ_id_comp]
  ¬∑ dsimp [Œ≤']
    rw [comp_hom, mk‚ÇÄ_hom, mk‚ÇÄ_hom]
    dsimp
    rw [ShiftedHom.mk‚ÇÄ_comp_mk‚ÇÄ_assoc, ‚Üê Functor.map_comp,
      biprod.lift_snd, Functor.map_id, ShiftedHom.mk‚ÇÄ_id_comp]

lemma neg_hom (Œ± : Ext X Y n) : (-Œ±).hom = -Œ±.hom := by
  rw [‚Üê add_right_inj Œ±.hom, ‚Üê add_hom, add_right_neg, add_right_neg, zero_hom]

/-- When an instance of `[HasDerivedCategory.{w'} C]` is available, this is the additive
bijection between `Ext.{w} X Y n` and a type of morphisms in the derived category. -/
noncomputable def homAddEquiv {n : ‚Ñï} :
    Ext.{w} X Y n ‚âÉ+
      ShiftedHom ((singleFunctor C 0).obj X) ((singleFunctor C 0).obj Y) (n : ‚Ñ§) where
  toEquiv := homEquiv
  map_add' := by simp

@[simp]
lemma homAddEquiv_apply (Œ± : Ext X Y n) : homAddEquiv Œ± = Œ±.hom := rfl

end

@[simps!]
noncomputable def postcomp (Œ≤ : Ext Y Z n) (X : C) {a b : ‚Ñï} (h : a + n = b) :
    Ext X Y a ‚Üí+ Ext X Z b :=
  AddMonoidHom.mk' (fun Œ± => Œ±.comp Œ≤ h) (by simp)

@[simps!]
noncomputable def precomp (Œ± : Ext X Y n) (Z : C) {a b : ‚Ñï} (h : n + a = b) :
    Ext Y Z a ‚Üí+ Ext X Z b :=
  AddMonoidHom.mk' (fun Œ≤ => Œ±.comp Œ≤ h) (by simp)

end Ext

end Abelian

open Abelian

namespace ShortComplex

variable (S : ShortComplex C)

lemma ext_mk‚ÇÄ_f_comp_ext_mk‚ÇÄ_g : (Ext.mk‚ÇÄ S.f).comp (Ext.mk‚ÇÄ S.g) (zero_add 0) = 0 := by simp

namespace ShortExact

variable {S}
variable (hS : S.ShortExact)

/-- The class in `Ext S.X‚ÇÉ S.X‚ÇÅ 1` that is attached to a short exact
short complex `S` in an abelian category. -/
noncomputable def extClass : Ext.{w} S.X‚ÇÉ S.X‚ÇÅ 1 := by
  let W := (HomologicalComplex.quasiIso C (ComplexShape.up ‚Ñ§))
  let S' := S.map (CochainComplex.singleFunctor C 0)
  have hS' : S'.ShortExact := hS.map_of_exact (HomologicalComplex.single _ _ _)
  let K := CochainComplex.mappingCone S'.f
  let w : K ‚ü∂ S'.X‚ÇÉ := CochainComplex.mappingCone.descShortComplex S'
  have hw : W w := CochainComplex.mappingCone.quasiIso_descShortComplex hS'
  let Œ¥ : K ‚ü∂ S'.X‚ÇÅ‚ü¶(1 : ‚Ñ§)‚üß := (CochainComplex.mappingCone.triangle S'.f).mor‚ÇÉ
  have : HasSmallLocalizedShiftedHom.{w} W ‚Ñ§ S'.X‚ÇÉ S'.X‚ÇÅ := by
    dsimp [W, S']
    infer_instance
  have : HasSmallLocalizedHom.{w} W S'.X‚ÇÉ K := by
    rw [Localization.hasSmallLocalizedHom_iff_target W S'.X‚ÇÉ w hw]
    dsimp [W, S']
    apply Localization.hasSmallLocalizedHom_of_hasSmallLocalizedShiftedHom‚ÇÄ (M := ‚Ñ§)
  have : HasSmallLocalizedShiftedHom.{w} W ‚Ñ§ K S'.X‚ÇÅ := by
    rw [Localization.hasSmallLocalizedShiftedHom_iff_source.{w} W ‚Ñ§ w hw S'.X‚ÇÅ]
    dsimp [W, S']
    infer_instance
  change SmallHom W S'.X‚ÇÉ (S'.X‚ÇÅ‚ü¶(1 : ‚Ñ§)‚üß)
  exact (SmallHom.mkInv w hw).comp (SmallHom.mk W Œ¥)

@[simp]
lemma extClass_hom [HasDerivedCategory.{w'} C] : hS.extClass.hom = hS.singleŒ¥ := by
  let W := (HomologicalComplex.quasiIso C (ComplexShape.up ‚Ñ§))
  let S' := S.map (CochainComplex.singleFunctor C 0)
  have hS' : S'.ShortExact := hS.map_of_exact (HomologicalComplex.single _ _ _)
  let K := CochainComplex.mappingCone S'.f
  let w : K ‚ü∂ S'.X‚ÇÉ := CochainComplex.mappingCone.descShortComplex S'
  have hw : W w := CochainComplex.mappingCone.quasiIso_descShortComplex hS'
  let Œ¥ : K ‚ü∂ S'.X‚ÇÅ‚ü¶(1 : ‚Ñ§)‚üß := (CochainComplex.mappingCone.triangle S'.f).mor‚ÇÉ
  have : HasSmallLocalizedShiftedHom.{w} W ‚Ñ§ S'.X‚ÇÉ S'.X‚ÇÅ := by
    dsimp [W, S']
    infer_instance
  have : HasSmallLocalizedHom.{w} W S'.X‚ÇÉ K := by
    rw [Localization.hasSmallLocalizedHom_iff_target W S'.X‚ÇÉ w hw]
    dsimp [W, S']
    apply Localization.hasSmallLocalizedHom_of_hasSmallLocalizedShiftedHom‚ÇÄ (M := ‚Ñ§)
  have : HasSmallLocalizedShiftedHom.{w} W ‚Ñ§ K S'.X‚ÇÅ := by
    rw [Localization.hasSmallLocalizedShiftedHom_iff_source.{w} W ‚Ñ§ w hw S'.X‚ÇÅ]
    dsimp [W, S']
    infer_instance
  change SmallShiftedHom.equiv W Q ((SmallHom.mkInv w hw).comp (SmallHom.mk W Œ¥)) = _
  dsimp [SmallShiftedHom.equiv]
  erw [SmallHom.equiv_comp, Iso.homToEquiv_apply]
  rw [SmallHom.equiv_mkInv, SmallHom.equiv_mk]
  simp
  dsimp [singleŒ¥, triangleOfSESŒ¥, Œ¥, w, S']
  rw [Category.assoc, singleFunctorsPostcompQIso_hom_hom,
    singleFunctorsPostcompQIso_inv_hom]
  erw [Category.id_comp, Functor.map_id, Category.comp_id]
  rfl

@[simp]
lemma comp_extClass : (Ext.mk‚ÇÄ S.g).comp hS.extClass (zero_add 1) = 0 := by
  letI := HasDerivedCategory.standard C
  ext
  simp only [Ext.comp_hom, Ext.mk‚ÇÄ_hom, extClass_hom, Ext.zero_hom,
    ShiftedHom.mk‚ÇÄ_comp]
  exact comp_distTriang_mor_zero‚ÇÇ‚ÇÉ _ hS.singleTriangle_distinguished

@[simp]
lemma extClass_comp : hS.extClass.comp (Ext.mk‚ÇÄ S.f) (add_zero 1) = 0 := by
  letI := HasDerivedCategory.standard C
  ext
  simp only [Ext.comp_hom, Ext.mk‚ÇÄ_hom, extClass_hom, Ext.zero_hom,
    ShiftedHom.comp_mk‚ÇÄ]
  exact comp_distTriang_mor_zero‚ÇÉ‚ÇÅ _ hS.singleTriangle_distinguished

end ShortExact

end ShortComplex

end CategoryTheory
