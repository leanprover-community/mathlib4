/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.DerivedCategory.Basic
import Mathlib.CategoryTheory.Localization.SmallShiftedHom

/-!
# Ext groups in abelian categories

Let `C` be an abelian category (with `C : Type u` and `Category.{v} C`).
In this file, we introduce the assumption `HasExt.{w} C` which asserts
that morphisms between single complexes in arbitrary degrees in
the derived category of `C` are `w`-small. Under this assumption,
we define `Ext.{w} X Y n : Type w` as shrunk versions of suitable
types of morphisms in the derived category. In particular, when `C` has
enough projectives or enough injectives, the property `HasExt.{v} C`
shall hold (TODO).

Note: in certain situations, `w := v` shall be the preferred
choice of universe (e.g. if `C := ModuleCat.{v} R` with `R : Type v`).
However, in the development of the API for Ext-groups, it is important
to keep a larger degree of generality for universes, as `w < v`
may happen in certain situations. Indeed, if `X : Scheme.{u}`,
then the underlying category of the Ã©tale site of `X` shall be a large
category. However, the category `Sheaf X.Etale AddCommGroupCat.{u}`
shall have good properties (because there is a small category of affine
schemes with the same category of sheaves), and even though the type of
morphisms in `Sheaf X.Etale AddCommGroupCat.{u}` shall be
in `Type (u + 1)`, these types are going to be `u`-small.
Then, for `C := Sheaf X.etale AddCommGroupCat.{u}`, we will have
`Category.{u + 1} C`, but `HasExt.{u} C` will hold
(as `C` has enough injectives). Then, the `Ext` groups between Ã©tale
sheaves over `X` shall be in `Type u`.

## TODO
* construct the additive structure on `Ext`
* compute `Ext X Y 0`
* define the class in `Ext S.Xâ‚ƒ S.Xâ‚ 1` of a short exact short complex `S`
* construct the long exact sequences of `Ext`.

-/

universe w' w v u

namespace CategoryTheory

variable (C : Type u) [Category.{v} C] [Abelian C]

open Localization Limits

/-- The property that morphisms between single complexes in arbitrary degrees are `w`-small
in the derived category. -/
abbrev HasExt : Prop :=
  âˆ€ (X Y : C), HasSmallLocalizedShiftedHom.{w} (HomologicalComplex.quasiIso C (ComplexShape.up â„¤)) â„¤
    ((CochainComplex.singleFunctor C 0).obj X) ((CochainComplex.singleFunctor C 0).obj Y)

-- TODO: when the canonical t-structure is formalized, replace `n : â„¤` by `n : â„•`
lemma hasExt_iff [HasDerivedCategory.{w'} C] :
    HasExt.{w} C â†” âˆ€ (X Y : C) (n : â„¤), Small.{w}
      ((DerivedCategory.singleFunctor C 0).obj X âŸ¶
        (((DerivedCategory.singleFunctor C 0).obj Y)âŸ¦nâŸ§)) := by
  dsimp [HasExt]
  simp only [hasSmallLocalizedShiftedHom_iff _ _ DerivedCategory.Q]
  constructor
  Â· intro h X Y n
    exact (small_congr ((shiftFunctorZero _ â„¤).app
      ((DerivedCategory.singleFunctor C 0).obj X)).homFromEquiv).1 (h X Y 0 n)
  Â· intro h X Y a b
    refine (small_congr ?_).1 (h X Y (b - a))
    exact (Functor.FullyFaithful.ofFullyFaithful
      (shiftFunctor _ a)).homEquiv.trans
      ((shiftFunctorAdd' _ _ _ _ (Int.sub_add_cancel b a)).symm.app _).homToEquiv

lemma hasExt_of_hasDerivedCategory [HasDerivedCategory.{w} C] : HasExt.{w} C := by
  rw [hasExt_iff.{w}]
  infer_instance

variable {C}

variable [HasExt.{w} C]

namespace Abelian

/-- A Ext-group in an abelian category `C`, defined as a `Type w` when `[HasExt.{w} C]`. -/
def Ext (X Y : C) (n : â„•) : Type w :=
  SmallShiftedHom.{w} (HomologicalComplex.quasiIso C (ComplexShape.up â„¤))
    ((CochainComplex.singleFunctor C 0).obj X)
    ((CochainComplex.singleFunctor C 0).obj Y) (n : â„¤)

namespace Ext

variable {X Y Z T : C}

/-- The composition of `Ext`. -/
noncomputable def comp {a b : â„•} (Î± : Ext X Y a) (Î² : Ext Y Z b) {c : â„•} (h : a + b = c) :
    Ext X Z c :=
  SmallShiftedHom.comp Î± Î² (by omega)

lemma comp_assoc {aâ‚ aâ‚‚ aâ‚ƒ aâ‚â‚‚ aâ‚‚â‚ƒ a : â„•} (Î± : Ext X Y aâ‚) (Î² : Ext Y Z aâ‚‚) (Î³ : Ext Z T aâ‚ƒ)
    (hâ‚â‚‚ : aâ‚ + aâ‚‚ = aâ‚â‚‚) (hâ‚‚â‚ƒ : aâ‚‚ + aâ‚ƒ = aâ‚‚â‚ƒ) (h : aâ‚ + aâ‚‚ + aâ‚ƒ = a) :
    (Î±.comp Î² hâ‚â‚‚).comp Î³ (show aâ‚â‚‚ + aâ‚ƒ = a by omega) =
      Î±.comp (Î².comp Î³ hâ‚‚â‚ƒ) (by omega) :=
  SmallShiftedHom.comp_assoc _ _ _ _ _ _ (by omega)

section

variable [HasDerivedCategory.{w'} C]

/-- When an instance of `[HasDerivedCategory.{w'} C]` is available, this is the bijection
between `Ext.{w} X Y n` and a type of morphisms in the derived category. -/
noncomputable def homEquiv {n : â„•} :
    Ext.{w} X Y n â‰ƒ ShiftedHom ((DerivedCategory.singleFunctor C 0).obj X)
      ((DerivedCategory.singleFunctor C 0).obj Y) (n : â„¤) :=
  SmallShiftedHom.equiv (HomologicalComplex.quasiIso C (ComplexShape.up â„¤)) DerivedCategory.Q

/-- The morphism in the derived category which corresponds to an element in `Ext X Y a`. -/
noncomputable abbrev hom {a : â„•} (Î± : Ext X Y a) :
    ShiftedHom ((DerivedCategory.singleFunctor C 0).obj X)
      ((DerivedCategory.singleFunctor C 0).obj Y) (a : â„¤) :=
  homEquiv Î±

@[simp]
lemma comp_hom {a b : â„•} (Î± : Ext X Y a) (Î² : Ext Y Z b) {c : â„•} (h : a + b = c) :
    (Î±.comp Î² h).hom = Î±.hom.comp Î².hom (by omega) := by
  apply SmallShiftedHom.equiv_comp

@[ext]
lemma ext {n : â„•} {Î± Î² : Ext X Y n} (h : Î±.hom = Î².hom) : Î± = Î² :=
  homEquiv.injective h

lemma ext_iff {n : â„•} {Î± Î² : Ext X Y n} : Î± = Î² â†” Î±.hom = Î².hom :=
  âŸ¨fun h â†¦ by rw [h], extâŸ©

end

noncomputable def mkâ‚€ (f : X âŸ¶ Y) : Ext X Y 0 := SmallShiftedHom.mkâ‚€ _ _ (by simp)
  ((CochainComplex.singleFunctor C 0).map f)

@[simp]
lemma mkâ‚€_hom [HasDerivedCategory.{w'} C] (f : X âŸ¶ Y) :
    (mkâ‚€ f).hom = ShiftedHom.mkâ‚€ _ (by simp) ((DerivedCategory.singleFunctor C 0).map f) :=
  sorry

section

variable {n : â„•} {Xâ‚ Xâ‚‚ : C}

instance [HasDerivedCategory C] :
    PreservesBinaryBiproduct Xâ‚ Xâ‚‚ (DerivedCategory.singleFunctor C 0) :=
  sorry

lemma biprod_ext {Î± Î² : Ext (Xâ‚ âŠž Xâ‚‚) Y n}
    (hâ‚ : (mkâ‚€ biprod.inl).comp Î± (zero_add n) = (mkâ‚€ biprod.inl).comp Î² (zero_add n))
    (hâ‚‚ : (mkâ‚€ biprod.inr).comp Î± (zero_add n) = (mkâ‚€ biprod.inr).comp Î² (zero_add n)) :
    Î± = Î² := by
  letI := HasDerivedCategory.standard C
  rw [ext_iff] at hâ‚ hâ‚‚ âŠ¢
  simp only [comp_hom, mkâ‚€_hom, ShiftedHom.mkâ‚€_comp] at hâ‚ hâ‚‚
  apply BinaryCofan.IsColimit.hom_ext
    (isBinaryBilimitOfPreserves (DerivedCategory.singleFunctor C 0)
      (BinaryBiproduct.isBilimit Xâ‚ Xâ‚‚)).isColimit
  all_goals assumption

variable (Î±â‚ : Ext Xâ‚ Y n) (Î±â‚‚ : Ext Xâ‚‚ Y n)

noncomputable def descBiprod : Ext (Xâ‚ âŠž Xâ‚‚) Y n := by
  letI := HasDerivedCategory.standard C
  exact homEquiv.symm (Cofan.IsColimit.desc
    (isBinaryBilimitOfPreserves (DerivedCategory.singleFunctor C 0)
      (BinaryBiproduct.isBilimit Xâ‚ Xâ‚‚)).isColimit (by
        rintro âŸ¨_|_âŸ©
        Â· exact Î±â‚.hom
        Â· exact Î±â‚‚.hom))

@[simp]
lemma inl_descBiprod : (mkâ‚€ biprod.inl).comp (descBiprod Î±â‚ Î±â‚‚) (zero_add n) = Î±â‚ := by
  letI := HasDerivedCategory.standard C
  ext
  dsimp [descBiprod]
  simp only [comp_hom, Int.Nat.cast_ofNat_Int, mkâ‚€_hom, Equiv.apply_symm_apply,
    ShiftedHom.mkâ‚€_comp]
  exact Cofan.IsColimit.fac
    (isBinaryBilimitOfPreserves (DerivedCategory.singleFunctor C 0)
      (BinaryBiproduct.isBilimit Xâ‚ Xâ‚‚)).isColimit _ WalkingPair.left

@[simp]
lemma inr_descBiprod : (mkâ‚€ biprod.inr).comp (descBiprod Î±â‚ Î±â‚‚) (zero_add n) = Î±â‚‚ := by
  letI := HasDerivedCategory.standard C
  ext
  dsimp [descBiprod]
  simp only [comp_hom, Int.Nat.cast_ofNat_Int, mkâ‚€_hom, Equiv.apply_symm_apply,
    ShiftedHom.mkâ‚€_comp]
  exact Cofan.IsColimit.fac
    (isBinaryBilimitOfPreserves (DerivedCategory.singleFunctor C 0)
      (BinaryBiproduct.isBilimit Xâ‚ Xâ‚‚)).isColimit _ WalkingPair.right

end

noncomputable instance {n : â„•} : Add (Ext X Y n) where
  add Î±â‚ Î±â‚‚ := (mkâ‚€ (biprod.lift (ðŸ™ X) (ðŸ™ X))).comp (descBiprod Î±â‚ Î±â‚‚) (zero_add n)

lemma add_hom [HasDerivedCategory.{w'} C] {n : â„•} (Î±â‚ Î±â‚‚ : Ext X Y n) :
    (Î±â‚ + Î±â‚‚).hom = Î±â‚.hom + Î±â‚‚.hom :=
  sorry

end Ext

end Abelian

end CategoryTheory
