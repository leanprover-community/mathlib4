/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Scott Morrison
-/
import Mathlib.Algebra.Homology.ComplexShape
import Mathlib.CategoryTheory.Subobject.Limits
import Mathlib.CategoryTheory.GradedObject
import Mathlib.Algebra.Homology.ShortComplex.Basic

#align_import algebra.homology.homological_complex from "leanprover-community/mathlib"@"88bca0ce5d22ebfd9e73e682e51d60ea13b48347"

/-!
# Homological complexes.

A `HomologicalComplex V c` with a "shape" controlled by `c : ComplexShape Œπ`
has chain groups `X i` (objects in `V`) indexed by `i : Œπ`,
and a differential `d i j` whenever `c.Rel i j`.

We in fact ask for differentials `d i j` for all `i j : Œπ`,
but have a field `shape` requiring that these are zero when not allowed by `c`.
This avoids a lot of dependent type theory hell!

The composite of any two differentials `d i j ‚â´ d j k` must be zero.

We provide `ChainComplex V Œ±` for
`Œ±`-indexed chain complexes in which `d i j ‚â† 0` only if `j + 1 = i`,
and similarly `CochainComplex V Œ±`, with `i = j + 1`.

There is a category structure, where morphisms are chain maps.

For `C : HomologicalComplex V c`, we define `C.xNext i`, which is either `C.X j` for some
arbitrarily chosen `j` such that `c.r i j`, or `C.X i` if there is no such `j`.
Similarly we have `C.xPrev j`.
Defined in terms of these we have `C.dFrom i : C.X i ‚ü∂ C.xNext i` and
`C.dTo j : C.xPrev j ‚ü∂ C.X j`, which are either defined as `C.d i j`, or zero, as needed.
-/


universe v u

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits

variable {Œπ : Type*}
variable (V : Type u) [Category.{v} V] [HasZeroMorphisms V]

/-- A `HomologicalComplex V c` with a "shape" controlled by `c : ComplexShape Œπ`
has chain groups `X i` (objects in `V`) indexed by `i : Œπ`,
and a differential `d i j` whenever `c.Rel i j`.

We in fact ask for differentials `d i j` for all `i j : Œπ`,
but have a field `shape` requiring that these are zero when not allowed by `c`.
This avoids a lot of dependent type theory hell!

The composite of any two differentials `d i j ‚â´ d j k` must be zero.
-/
structure HomologicalComplex (c : ComplexShape Œπ) where
  X : Œπ ‚Üí V
  d : ‚àÄ i j, X i ‚ü∂ X j
  shape : ‚àÄ i j, ¬¨c.Rel i j ‚Üí d i j = 0 := by aesop_cat
  d_comp_d' : ‚àÄ i j k, c.Rel i j ‚Üí c.Rel j k ‚Üí d i j ‚â´ d j k = 0 := by aesop_cat
#align homological_complex HomologicalComplex

namespace HomologicalComplex

attribute [simp] shape

variable {V} {c : ComplexShape Œπ}

@[reassoc (attr := simp)]
theorem d_comp_d (C : HomologicalComplex V c) (i j k : Œπ) : C.d i j ‚â´ C.d j k = 0 := by
  by_cases hij : c.Rel i j
  ¬∑ by_cases hjk : c.Rel j k
    ¬∑ exact C.d_comp_d' i j k hij hjk
    ¬∑ rw [C.shape j k hjk, comp_zero]
  ¬∑ rw [C.shape i j hij, zero_comp]
#align homological_complex.d_comp_d HomologicalComplex.d_comp_d

theorem ext {C‚ÇÅ C‚ÇÇ : HomologicalComplex V c} (h_X : C‚ÇÅ.X = C‚ÇÇ.X)
    (h_d :
      ‚àÄ i j : Œπ,
        c.Rel i j ‚Üí C‚ÇÅ.d i j ‚â´ eqToHom (congr_fun h_X j) = eqToHom (congr_fun h_X i) ‚â´ C‚ÇÇ.d i j) :
    C‚ÇÅ = C‚ÇÇ := by
  obtain ‚ü®X‚ÇÅ, d‚ÇÅ, s‚ÇÅ, h‚ÇÅ‚ü© := C‚ÇÅ
  obtain ‚ü®X‚ÇÇ, d‚ÇÇ, s‚ÇÇ, h‚ÇÇ‚ü© := C‚ÇÇ
  dsimp at h_X
  subst h_X
  simp only [mk.injEq, heq_eq_eq, true_and]
  ext i j
  by_cases hij: c.Rel i j
  ¬∑ simpa only [comp_id, id_comp, eqToHom_refl] using h_d i j hij
  ¬∑ rw [s‚ÇÅ i j hij, s‚ÇÇ i j hij]
#align homological_complex.ext HomologicalComplex.ext

/-- The obvious isomorphism `K.X p ‚âÖ K.X q` when `p = q`. -/
def XIsoOfEq (K : HomologicalComplex V c) {p q : Œπ} (h : p = q) : K.X p ‚âÖ K.X q :=
  eqToIso (by rw [h])

@[simp]
lemma XIsoOfEq_rfl (K : HomologicalComplex V c) (p : Œπ) :
    K.XIsoOfEq (rfl : p = p) = Iso.refl _ := rfl

@[reassoc (attr := simp)]
lemma XIsoOfEq_hom_comp_XIsoOfEq_hom (K : HomologicalComplex V c) {p‚ÇÅ p‚ÇÇ p‚ÇÉ : Œπ}
    (h‚ÇÅ‚ÇÇ : p‚ÇÅ = p‚ÇÇ) (h‚ÇÇ‚ÇÉ : p‚ÇÇ = p‚ÇÉ) :
    (K.XIsoOfEq h‚ÇÅ‚ÇÇ).hom ‚â´ (K.XIsoOfEq h‚ÇÇ‚ÇÉ).hom = (K.XIsoOfEq (h‚ÇÅ‚ÇÇ.trans h‚ÇÇ‚ÇÉ)).hom := by
  dsimp [XIsoOfEq]
  simp only [eqToHom_trans]

@[reassoc (attr := simp)]
lemma XIsoOfEq_hom_comp_XIsoOfEq_inv (K : HomologicalComplex V c) {p‚ÇÅ p‚ÇÇ p‚ÇÉ : Œπ}
    (h‚ÇÅ‚ÇÇ : p‚ÇÅ = p‚ÇÇ) (h‚ÇÉ‚ÇÇ : p‚ÇÉ = p‚ÇÇ) :
    (K.XIsoOfEq h‚ÇÅ‚ÇÇ).hom ‚â´ (K.XIsoOfEq h‚ÇÉ‚ÇÇ).inv = (K.XIsoOfEq (h‚ÇÅ‚ÇÇ.trans h‚ÇÉ‚ÇÇ.symm)).hom := by
  dsimp [XIsoOfEq]
  simp only [eqToHom_trans]

@[reassoc (attr := simp)]
lemma XIsoOfEq_inv_comp_XIsoOfEq_hom (K : HomologicalComplex V c) {p‚ÇÅ p‚ÇÇ p‚ÇÉ : Œπ}
    (h‚ÇÇ‚ÇÅ : p‚ÇÇ = p‚ÇÅ) (h‚ÇÇ‚ÇÉ : p‚ÇÇ = p‚ÇÉ) :
    (K.XIsoOfEq h‚ÇÇ‚ÇÅ).inv ‚â´ (K.XIsoOfEq h‚ÇÇ‚ÇÉ).hom = (K.XIsoOfEq (h‚ÇÇ‚ÇÅ.symm.trans h‚ÇÇ‚ÇÉ)).hom := by
  dsimp [XIsoOfEq]
  simp only [eqToHom_trans]

@[reassoc (attr := simp)]
lemma XIsoOfEq_inv_comp_XIsoOfEq_inv (K : HomologicalComplex V c) {p‚ÇÅ p‚ÇÇ p‚ÇÉ : Œπ}
    (h‚ÇÇ‚ÇÅ : p‚ÇÇ = p‚ÇÅ) (h‚ÇÉ‚ÇÇ : p‚ÇÉ = p‚ÇÇ) :
    (K.XIsoOfEq h‚ÇÇ‚ÇÅ).inv ‚â´ (K.XIsoOfEq h‚ÇÉ‚ÇÇ).inv = (K.XIsoOfEq (h‚ÇÉ‚ÇÇ.trans h‚ÇÇ‚ÇÅ).symm).hom := by
  dsimp [XIsoOfEq]
  simp only [eqToHom_trans]

@[reassoc (attr := simp)]
lemma XIsoOfEq_hom_comp_d (K : HomologicalComplex V c) {p‚ÇÅ p‚ÇÇ : Œπ} (h : p‚ÇÅ = p‚ÇÇ) (p‚ÇÉ : Œπ) :
    (K.XIsoOfEq h).hom ‚â´ K.d p‚ÇÇ p‚ÇÉ = K.d p‚ÇÅ p‚ÇÉ := by subst h; simp

@[reassoc (attr := simp)]
lemma XIsoOfEq_inv_comp_d (K : HomologicalComplex V c) {p‚ÇÇ p‚ÇÅ : Œπ} (h : p‚ÇÇ = p‚ÇÅ) (p‚ÇÉ : Œπ) :
    (K.XIsoOfEq h).inv ‚â´ K.d p‚ÇÇ p‚ÇÉ = K.d p‚ÇÅ p‚ÇÉ := by subst h; simp

@[reassoc (attr := simp)]
lemma d_comp_XIsoOfEq_hom (K : HomologicalComplex V c) {p‚ÇÇ p‚ÇÉ : Œπ} (h : p‚ÇÇ = p‚ÇÉ) (p‚ÇÅ : Œπ) :
    K.d p‚ÇÅ p‚ÇÇ ‚â´ (K.XIsoOfEq h).hom = K.d p‚ÇÅ p‚ÇÉ := by subst h; simp

@[reassoc (attr := simp)]
lemma d_comp_XIsoOfEq_inv (K : HomologicalComplex V c) {p‚ÇÇ p‚ÇÉ : Œπ} (h : p‚ÇÉ = p‚ÇÇ) (p‚ÇÅ : Œπ) :
    K.d p‚ÇÅ p‚ÇÇ ‚â´ (K.XIsoOfEq h).inv = K.d p‚ÇÅ p‚ÇÉ := by subst h; simp

end HomologicalComplex

/-- An `Œ±`-indexed chain complex is a `HomologicalComplex`
in which `d i j ‚â† 0` only if `j + 1 = i`.
-/
abbrev ChainComplex (Œ± : Type*) [AddRightCancelSemigroup Œ±] [One Œ±] : Type _ :=
  HomologicalComplex V (ComplexShape.down Œ±)
#align chain_complex ChainComplex

/-- An `Œ±`-indexed cochain complex is a `HomologicalComplex`
in which `d i j ‚â† 0` only if `i + 1 = j`.
-/
abbrev CochainComplex (Œ± : Type*) [AddRightCancelSemigroup Œ±] [One Œ±] : Type _ :=
  HomologicalComplex V (ComplexShape.up Œ±)
#align cochain_complex CochainComplex

namespace ChainComplex

@[simp]
theorem prev (Œ± : Type*) [AddRightCancelSemigroup Œ±] [One Œ±] (i : Œ±) :
    (ComplexShape.down Œ±).prev i = i + 1 :=
  (ComplexShape.down Œ±).prev_eq' rfl
#align chain_complex.prev ChainComplex.prev

@[simp]
theorem next (Œ± : Type*) [AddGroup Œ±] [One Œ±] (i : Œ±) : (ComplexShape.down Œ±).next i = i - 1 :=
  (ComplexShape.down Œ±).next_eq' <| sub_add_cancel _ _
#align chain_complex.next ChainComplex.next

@[simp]
theorem next_nat_zero : (ComplexShape.down ‚Ñï).next 0 = 0 := by
  classical
    refine dif_neg ?_
    push_neg
    intro
    apply Nat.noConfusion
#align chain_complex.next_nat_zero ChainComplex.next_nat_zero

@[simp]
theorem next_nat_succ (i : ‚Ñï) : (ComplexShape.down ‚Ñï).next (i + 1) = i :=
  (ComplexShape.down ‚Ñï).next_eq' rfl
#align chain_complex.next_nat_succ ChainComplex.next_nat_succ

end ChainComplex

namespace CochainComplex

@[simp]
theorem prev (Œ± : Type*) [AddGroup Œ±] [One Œ±] (i : Œ±) : (ComplexShape.up Œ±).prev i = i - 1 :=
  (ComplexShape.up Œ±).prev_eq' <| sub_add_cancel _ _
#align cochain_complex.prev CochainComplex.prev

@[simp]
theorem next (Œ± : Type*) [AddRightCancelSemigroup Œ±] [One Œ±] (i : Œ±) :
    (ComplexShape.up Œ±).next i = i + 1 :=
  (ComplexShape.up Œ±).next_eq' rfl
#align cochain_complex.next CochainComplex.next

@[simp]
theorem prev_nat_zero : (ComplexShape.up ‚Ñï).prev 0 = 0 := by
  classical
    refine dif_neg ?_
    push_neg
    intro
    apply Nat.noConfusion
#align cochain_complex.prev_nat_zero CochainComplex.prev_nat_zero

@[simp]
theorem prev_nat_succ (i : ‚Ñï) : (ComplexShape.up ‚Ñï).prev (i + 1) = i :=
  (ComplexShape.up ‚Ñï).prev_eq' rfl
#align cochain_complex.prev_nat_succ CochainComplex.prev_nat_succ

end CochainComplex

namespace HomologicalComplex

variable {V}
variable {c : ComplexShape Œπ} (C : HomologicalComplex V c)

/-- A morphism of homological complexes consists of maps between the chain groups,
commuting with the differentials.
-/
@[ext]
structure Hom (A B : HomologicalComplex V c) where
  f : ‚àÄ i, A.X i ‚ü∂ B.X i
  comm' : ‚àÄ i j, c.Rel i j ‚Üí f i ‚â´ B.d i j = A.d i j ‚â´ f j := by aesop_cat
#align homological_complex.hom HomologicalComplex.Hom

@[reassoc (attr := simp)]
theorem Hom.comm {A B : HomologicalComplex V c} (f : A.Hom B) (i j : Œπ) :
    f.f i ‚â´ B.d i j = A.d i j ‚â´ f.f j := by
  by_cases hij : c.Rel i j
  ¬∑ exact f.comm' i j hij
  ¬∑ rw [A.shape i j hij, B.shape i j hij, comp_zero, zero_comp]
#align homological_complex.hom.comm HomologicalComplex.Hom.comm

instance (A B : HomologicalComplex V c) : Inhabited (Hom A B) :=
  ‚ü®{ f := fun i => 0 }‚ü©

/-- Identity chain map. -/
def id (A : HomologicalComplex V c) : Hom A A where f _ := ùüô _
#align homological_complex.id HomologicalComplex.id

/-- Composition of chain maps. -/
def comp (A B C : HomologicalComplex V c) (œÜ : Hom A B) (œà : Hom B C) : Hom A C where
  f i := œÜ.f i ‚â´ œà.f i
#align homological_complex.comp HomologicalComplex.comp

section

attribute [local simp] id comp

instance : Category (HomologicalComplex V c) where
  Hom := Hom
  id := id
  comp := comp _ _ _

end

-- Porting note: added because `Hom.ext` is not triggered automatically
@[ext]
lemma hom_ext {C D : HomologicalComplex V c} (f g : C ‚ü∂ D)
    (h : ‚àÄ i, f.f i = g.f i) : f = g := by
  apply Hom.ext
  funext
  apply h

@[simp]
theorem id_f (C : HomologicalComplex V c) (i : Œπ) : Hom.f (ùüô C) i = ùüô (C.X i) :=
  rfl
#align homological_complex.id_f HomologicalComplex.id_f

@[simp, reassoc]
theorem comp_f {C‚ÇÅ C‚ÇÇ C‚ÇÉ : HomologicalComplex V c} (f : C‚ÇÅ ‚ü∂ C‚ÇÇ) (g : C‚ÇÇ ‚ü∂ C‚ÇÉ) (i : Œπ) :
    (f ‚â´ g).f i = f.f i ‚â´ g.f i :=
  rfl
#align homological_complex.comp_f HomologicalComplex.comp_f

@[simp]
theorem eqToHom_f {C‚ÇÅ C‚ÇÇ : HomologicalComplex V c} (h : C‚ÇÅ = C‚ÇÇ) (n : Œπ) :
    HomologicalComplex.Hom.f (eqToHom h) n =
      eqToHom (congr_fun (congr_arg HomologicalComplex.X h) n) := by
  subst h
  rfl
#align homological_complex.eq_to_hom_f HomologicalComplex.eqToHom_f

-- We'll use this later to show that `HomologicalComplex V c` is preadditive when `V` is.
theorem hom_f_injective {C‚ÇÅ C‚ÇÇ : HomologicalComplex V c} :
    Function.Injective fun f : Hom C‚ÇÅ C‚ÇÇ => f.f := by aesop_cat
#align homological_complex.hom_f_injective HomologicalComplex.hom_f_injective

instance (X Y : HomologicalComplex V c) : Zero (X ‚ü∂ Y) :=
  ‚ü®{ f := fun i => 0}‚ü©

@[simp]
theorem zero_f (C D : HomologicalComplex V c) (i : Œπ) : (0 : C ‚ü∂ D).f i = 0 :=
  rfl
#align homological_complex.zero_apply HomologicalComplex.zero_f

instance : HasZeroMorphisms (HomologicalComplex V c) where

open ZeroObject

/-- The zero complex -/
noncomputable def zero [HasZeroObject V] : HomologicalComplex V c where
  X _ := 0
  d _ _ := 0
#align homological_complex.zero HomologicalComplex.zero

theorem isZero_zero [HasZeroObject V] : IsZero (zero : HomologicalComplex V c) := by
  refine ‚ü®fun X => ‚ü®‚ü®‚ü®0‚ü©, fun f => ?_‚ü©‚ü©, fun X => ‚ü®‚ü®‚ü®0‚ü©, fun f => ?_‚ü©‚ü©‚ü©
  all_goals
    ext
    dsimp [zero]
    apply Subsingleton.elim
#align homological_complex.is_zero_zero HomologicalComplex.isZero_zero

instance [HasZeroObject V] : HasZeroObject (HomologicalComplex V c) :=
  ‚ü®‚ü®zero, isZero_zero‚ü©‚ü©

noncomputable instance [HasZeroObject V] : Inhabited (HomologicalComplex V c) :=
  ‚ü®zero‚ü©

theorem congr_hom {C D : HomologicalComplex V c} {f g : C ‚ü∂ D} (w : f = g) (i : Œπ) :
    f.f i = g.f i :=
  congr_fun (congr_arg Hom.f w) i
#align homological_complex.congr_hom HomologicalComplex.congr_hom

lemma mono_of_mono_f {K L : HomologicalComplex V c} (œÜ : K ‚ü∂ L)
    (hœÜ : ‚àÄ i, Mono (œÜ.f i)) : Mono œÜ where
  right_cancellation g h eq := by
    ext i
    rw [‚Üê cancel_mono (œÜ.f i)]
    exact congr_hom eq i

lemma epi_of_epi_f {K L : HomologicalComplex V c} (œÜ : K ‚ü∂ L)
    (hœÜ : ‚àÄ i, Epi (œÜ.f i)) : Epi œÜ where
  left_cancellation g h eq := by
    ext i
    rw [‚Üê cancel_epi (œÜ.f i)]
    exact congr_hom eq i

section

variable (V c)

/-- The functor picking out the `i`-th object of a complex. -/
@[simps]
def eval (i : Œπ) : HomologicalComplex V c ‚•§ V where
  obj C := C.X i
  map f := f.f i
#align homological_complex.eval HomologicalComplex.eval

/-- The functor forgetting the differential in a complex, obtaining a graded object. -/
@[simps]
def forget : HomologicalComplex V c ‚•§ GradedObject Œπ V where
  obj C := C.X
  map f := f.f
#align homological_complex.forget HomologicalComplex.forget

instance : (forget V c).Faithful where
  map_injective h := by
    ext i
    exact congr_fun h i

/-- Forgetting the differentials than picking out the `i`-th object is the same as
just picking out the `i`-th object. -/
@[simps!]
def forgetEval (i : Œπ) : forget V c ‚ãô GradedObject.eval i ‚âÖ eval V c i :=
  NatIso.ofComponents fun X => Iso.refl _
#align homological_complex.forget_eval HomologicalComplex.forgetEval

end

noncomputable section

@[reassoc]
lemma XIsoOfEq_hom_naturality {K L : HomologicalComplex V c} (œÜ : K ‚ü∂ L) {n n' : Œπ} (h : n = n') :
    œÜ.f n ‚â´ (L.XIsoOfEq h).hom = (K.XIsoOfEq h).hom ‚â´ œÜ.f n' := by subst h; simp

@[reassoc]
lemma XIsoOfEq_inv_naturality {K L : HomologicalComplex V c} (œÜ : K ‚ü∂ L) {n n' : Œπ} (h : n = n') :
    œÜ.f n' ‚â´ (L.XIsoOfEq h).inv = (K.XIsoOfEq h).inv ‚â´ œÜ.f n := by subst h; simp

-- Porting note: removed @[simp] as the linter complained
/-- If `C.d i j` and `C.d i j'` are both allowed, then we must have `j = j'`,
and so the differentials only differ by an `eqToHom`.
-/
theorem d_comp_eqToHom {i j j' : Œπ} (rij : c.Rel i j) (rij' : c.Rel i j') :
    C.d i j' ‚â´ eqToHom (congr_arg C.X (c.next_eq rij' rij)) = C.d i j := by
  obtain rfl := c.next_eq rij rij'
  simp only [eqToHom_refl, comp_id]
#align homological_complex.d_comp_eq_to_hom HomologicalComplex.d_comp_eqToHom

-- Porting note: removed @[simp] as the linter complained
/-- If `C.d i j` and `C.d i' j` are both allowed, then we must have `i = i'`,
and so the differentials only differ by an `eqToHom`.
-/
theorem eqToHom_comp_d {i i' j : Œπ} (rij : c.Rel i j) (rij' : c.Rel i' j) :
    eqToHom (congr_arg C.X (c.prev_eq rij rij')) ‚â´ C.d i' j = C.d i j := by
  obtain rfl := c.prev_eq rij rij'
  simp only [eqToHom_refl, id_comp]
#align homological_complex.eq_to_hom_comp_d HomologicalComplex.eqToHom_comp_d

theorem kernel_eq_kernel [HasKernels V] {i j j' : Œπ} (r : c.Rel i j) (r' : c.Rel i j') :
    kernelSubobject (C.d i j) = kernelSubobject (C.d i j') := by
  rw [‚Üê d_comp_eqToHom C r r']
  apply kernelSubobject_comp_mono
#align homological_complex.kernel_eq_kernel HomologicalComplex.kernel_eq_kernel

theorem image_eq_image [HasImages V] [HasEqualizers V] {i i' j : Œπ} (r : c.Rel i j)
    (r' : c.Rel i' j) : imageSubobject (C.d i j) = imageSubobject (C.d i' j) := by
  rw [‚Üê eqToHom_comp_d C r r']
  apply imageSubobject_iso_comp
#align homological_complex.image_eq_image HomologicalComplex.image_eq_image

section

/-- Either `C.X i`, if there is some `i` with `c.Rel i j`, or `C.X j`. -/
abbrev xPrev (j : Œπ) : V :=
  C.X (c.prev j)
set_option linter.uppercaseLean3 false in
#align homological_complex.X_prev HomologicalComplex.xPrev

/-- If `c.Rel i j`, then `C.xPrev j` is isomorphic to `C.X i`. -/
def xPrevIso {i j : Œπ} (r : c.Rel i j) : C.xPrev j ‚âÖ C.X i :=
  eqToIso <| by rw [‚Üê c.prev_eq' r]
set_option linter.uppercaseLean3 false in
#align homological_complex.X_prev_iso HomologicalComplex.xPrevIso

/-- If there is no `i` so `c.Rel i j`, then `C.xPrev j` is isomorphic to `C.X j`. -/
def xPrevIsoSelf {j : Œπ} (h : ¬¨c.Rel (c.prev j) j) : C.xPrev j ‚âÖ C.X j :=
  eqToIso <|
    congr_arg C.X
      (by
        dsimp [ComplexShape.prev]
        rw [dif_neg]
        push_neg; intro i hi
        have : c.prev j = i := c.prev_eq' hi
        rw [this] at h; contradiction)
set_option linter.uppercaseLean3 false in
#align homological_complex.X_prev_iso_self HomologicalComplex.xPrevIsoSelf

/-- Either `C.X j`, if there is some `j` with `c.rel i j`, or `C.X i`. -/
abbrev xNext (i : Œπ) : V :=
  C.X (c.next i)
set_option linter.uppercaseLean3 false in
#align homological_complex.X_next HomologicalComplex.xNext

/-- If `c.Rel i j`, then `C.xNext i` is isomorphic to `C.X j`. -/
def xNextIso {i j : Œπ} (r : c.Rel i j) : C.xNext i ‚âÖ C.X j :=
  eqToIso <| by rw [‚Üê c.next_eq' r]
set_option linter.uppercaseLean3 false in
#align homological_complex.X_next_iso HomologicalComplex.xNextIso

/-- If there is no `j` so `c.Rel i j`, then `C.xNext i` is isomorphic to `C.X i`. -/
def xNextIsoSelf {i : Œπ} (h : ¬¨c.Rel i (c.next i)) : C.xNext i ‚âÖ C.X i :=
  eqToIso <|
    congr_arg C.X
      (by
        dsimp [ComplexShape.next]
        rw [dif_neg]; rintro ‚ü®j, hj‚ü©
        have : c.next i = j := c.next_eq' hj
        rw [this] at h; contradiction)
set_option linter.uppercaseLean3 false in
#align homological_complex.X_next_iso_self HomologicalComplex.xNextIsoSelf

/-- The differential mapping into `C.X j`, or zero if there isn't one.
-/
abbrev dTo (j : Œπ) : C.xPrev j ‚ü∂ C.X j :=
  C.d (c.prev j) j
#align homological_complex.d_to HomologicalComplex.dTo

/-- The differential mapping out of `C.X i`, or zero if there isn't one.
-/
abbrev dFrom (i : Œπ) : C.X i ‚ü∂ C.xNext i :=
  C.d i (c.next i)
#align homological_complex.d_from HomologicalComplex.dFrom

theorem dTo_eq {i j : Œπ} (r : c.Rel i j) : C.dTo j = (C.xPrevIso r).hom ‚â´ C.d i j := by
  obtain rfl := c.prev_eq' r
  exact (Category.id_comp _).symm
#align homological_complex.d_to_eq HomologicalComplex.dTo_eq

@[simp]
theorem dTo_eq_zero {j : Œπ} (h : ¬¨c.Rel (c.prev j) j) : C.dTo j = 0 :=
  C.shape _ _ h
#align homological_complex.d_to_eq_zero HomologicalComplex.dTo_eq_zero

theorem dFrom_eq {i j : Œπ} (r : c.Rel i j) : C.dFrom i = C.d i j ‚â´ (C.xNextIso r).inv := by
  obtain rfl := c.next_eq' r
  exact (Category.comp_id _).symm
#align homological_complex.d_from_eq HomologicalComplex.dFrom_eq

@[simp]
theorem dFrom_eq_zero {i : Œπ} (h : ¬¨c.Rel i (c.next i)) : C.dFrom i = 0 :=
  C.shape _ _ h
#align homological_complex.d_from_eq_zero HomologicalComplex.dFrom_eq_zero

@[reassoc (attr := simp)]
theorem xPrevIso_comp_dTo {i j : Œπ} (r : c.Rel i j) : (C.xPrevIso r).inv ‚â´ C.dTo j = C.d i j := by
  simp [C.dTo_eq r]
set_option linter.uppercaseLean3 false in
#align homological_complex.X_prev_iso_comp_d_to HomologicalComplex.xPrevIso_comp_dTo

@[reassoc (attr := simp)]
theorem xPrevIsoSelf_comp_dTo {j : Œπ} (h : ¬¨c.Rel (c.prev j) j) :
    (C.xPrevIsoSelf h).inv ‚â´ C.dTo j = 0 := by simp [h]
set_option linter.uppercaseLean3 false in
#align homological_complex.X_prev_iso_self_comp_d_to HomologicalComplex.xPrevIsoSelf_comp_dTo

@[reassoc (attr := simp)]
theorem dFrom_comp_xNextIso {i j : Œπ} (r : c.Rel i j) :
    C.dFrom i ‚â´ (C.xNextIso r).hom = C.d i j := by
  simp [C.dFrom_eq r]
set_option linter.uppercaseLean3 false in
#align homological_complex.d_from_comp_X_next_iso HomologicalComplex.dFrom_comp_xNextIso

@[reassoc (attr := simp)]
theorem dFrom_comp_xNextIsoSelf {i : Œπ} (h : ¬¨c.Rel i (c.next i)) :
    C.dFrom i ‚â´ (C.xNextIsoSelf h).hom = 0 := by simp [h]
set_option linter.uppercaseLean3 false in
#align homological_complex.d_from_comp_X_next_iso_self HomologicalComplex.dFrom_comp_xNextIsoSelf

@[simp 1100]
theorem dTo_comp_dFrom (j : Œπ) : C.dTo j ‚â´ C.dFrom j = 0 :=
  C.d_comp_d _ _ _
#align homological_complex.d_to_comp_d_from HomologicalComplex.dTo_comp_dFrom

theorem kernel_from_eq_kernel [HasKernels V] {i j : Œπ} (r : c.Rel i j) :
    kernelSubobject (C.dFrom i) = kernelSubobject (C.d i j) := by
  rw [C.dFrom_eq r]
  apply kernelSubobject_comp_mono
#align homological_complex.kernel_from_eq_kernel HomologicalComplex.kernel_from_eq_kernel

theorem image_to_eq_image [HasImages V] [HasEqualizers V] {i j : Œπ} (r : c.Rel i j) :
    imageSubobject (C.dTo j) = imageSubobject (C.d i j) := by
  rw [C.dTo_eq r]
  apply imageSubobject_iso_comp
#align homological_complex.image_to_eq_image HomologicalComplex.image_to_eq_image

end

namespace Hom

variable {C‚ÇÅ C‚ÇÇ C‚ÇÉ : HomologicalComplex V c}

/-- The `i`-th component of an isomorphism of chain complexes. -/
@[simps!]
def isoApp (f : C‚ÇÅ ‚âÖ C‚ÇÇ) (i : Œπ) : C‚ÇÅ.X i ‚âÖ C‚ÇÇ.X i :=
  (eval V c i).mapIso f
#align homological_complex.hom.iso_app HomologicalComplex.Hom.isoApp

/-- Construct an isomorphism of chain complexes from isomorphism of the objects
which commute with the differentials. -/
@[simps]
def isoOfComponents (f : ‚àÄ i, C‚ÇÅ.X i ‚âÖ C‚ÇÇ.X i)
    (hf : ‚àÄ i j, c.Rel i j ‚Üí (f i).hom ‚â´ C‚ÇÇ.d i j = C‚ÇÅ.d i j ‚â´ (f j).hom := by aesop_cat) :
    C‚ÇÅ ‚âÖ C‚ÇÇ where
  hom :=
    { f := fun i => (f i).hom
      comm' := hf }
  inv :=
    { f := fun i => (f i).inv
      comm' := fun i j hij =>
        calc
          (f i).inv ‚â´ C‚ÇÅ.d i j = (f i).inv ‚â´ (C‚ÇÅ.d i j ‚â´ (f j).hom) ‚â´ (f j).inv := by simp
          _ = (f i).inv ‚â´ ((f i).hom ‚â´ C‚ÇÇ.d i j) ‚â´ (f j).inv := by rw [hf i j hij]
          _ = C‚ÇÇ.d i j ‚â´ (f j).inv := by simp }
  hom_inv_id := by
    ext i
    exact (f i).hom_inv_id
  inv_hom_id := by
    ext i
    exact (f i).inv_hom_id
#align homological_complex.hom.iso_of_components HomologicalComplex.Hom.isoOfComponents

@[simp]
theorem isoOfComponents_app (f : ‚àÄ i, C‚ÇÅ.X i ‚âÖ C‚ÇÇ.X i)
    (hf : ‚àÄ i j, c.Rel i j ‚Üí (f i).hom ‚â´ C‚ÇÇ.d i j = C‚ÇÅ.d i j ‚â´ (f j).hom) (i : Œπ) :
    isoApp (isoOfComponents f hf) i = f i := by
  ext
  simp
#align homological_complex.hom.iso_of_components_app HomologicalComplex.Hom.isoOfComponents_app

theorem isIso_of_components (f : C‚ÇÅ ‚ü∂ C‚ÇÇ) [‚àÄ n : Œπ, IsIso (f.f n)] : IsIso f :=
  (HomologicalComplex.Hom.isoOfComponents fun n => asIso (f.f n)).isIso_hom
#align homological_complex.hom.is_iso_of_components HomologicalComplex.Hom.isIso_of_components

/-! Lemmas relating chain maps and `dTo`/`dFrom`. -/


/-- `f.prev j` is `f.f i` if there is some `r i j`, and `f.f j` otherwise. -/
abbrev prev (f : Hom C‚ÇÅ C‚ÇÇ) (j : Œπ) : C‚ÇÅ.xPrev j ‚ü∂ C‚ÇÇ.xPrev j :=
  f.f _
#align homological_complex.hom.prev HomologicalComplex.Hom.prev

theorem prev_eq (f : Hom C‚ÇÅ C‚ÇÇ) {i j : Œπ} (w : c.Rel i j) :
    f.prev j = (C‚ÇÅ.xPrevIso w).hom ‚â´ f.f i ‚â´ (C‚ÇÇ.xPrevIso w).inv := by
  obtain rfl := c.prev_eq' w
  simp only [xPrevIso, eqToIso_refl, Iso.refl_hom, Iso.refl_inv, comp_id, id_comp]
#align homological_complex.hom.prev_eq HomologicalComplex.Hom.prev_eq

/-- `f.next i` is `f.f j` if there is some `r i j`, and `f.f j` otherwise. -/
abbrev next (f : Hom C‚ÇÅ C‚ÇÇ) (i : Œπ) : C‚ÇÅ.xNext i ‚ü∂ C‚ÇÇ.xNext i :=
  f.f _
#align homological_complex.hom.next HomologicalComplex.Hom.next

theorem next_eq (f : Hom C‚ÇÅ C‚ÇÇ) {i j : Œπ} (w : c.Rel i j) :
    f.next i = (C‚ÇÅ.xNextIso w).hom ‚â´ f.f j ‚â´ (C‚ÇÇ.xNextIso w).inv := by
  obtain rfl := c.next_eq' w
  simp only [xNextIso, eqToIso_refl, Iso.refl_hom, Iso.refl_inv, comp_id, id_comp]
#align homological_complex.hom.next_eq HomologicalComplex.Hom.next_eq

@[reassoc, elementwise] -- @[simp] -- Porting note (#10618): simp can prove this
theorem comm_from (f : Hom C‚ÇÅ C‚ÇÇ) (i : Œπ) : f.f i ‚â´ C‚ÇÇ.dFrom i = C‚ÇÅ.dFrom i ‚â´ f.next i :=
  f.comm _ _
#align homological_complex.hom.comm_from HomologicalComplex.Hom.comm_from

attribute [simp 1100] comm_from_assoc
attribute [simp] comm_from_apply

@[reassoc, elementwise] -- @[simp] -- Porting note (#10618): simp can prove this
theorem comm_to (f : Hom C‚ÇÅ C‚ÇÇ) (j : Œπ) : f.prev j ‚â´ C‚ÇÇ.dTo j = C‚ÇÅ.dTo j ‚â´ f.f j :=
  f.comm _ _
#align homological_complex.hom.comm_to HomologicalComplex.Hom.comm_to

attribute [simp 1100] comm_to_assoc
attribute [simp] comm_to_apply

/-- A morphism of chain complexes
induces a morphism of arrows of the differentials out of each object.
-/
def sqFrom (f : Hom C‚ÇÅ C‚ÇÇ) (i : Œπ) : Arrow.mk (C‚ÇÅ.dFrom i) ‚ü∂ Arrow.mk (C‚ÇÇ.dFrom i) :=
  Arrow.homMk (f.comm_from i)
#align homological_complex.hom.sq_from HomologicalComplex.Hom.sqFrom

@[simp]
theorem sqFrom_left (f : Hom C‚ÇÅ C‚ÇÇ) (i : Œπ) : (f.sqFrom i).left = f.f i :=
  rfl
#align homological_complex.hom.sq_from_left HomologicalComplex.Hom.sqFrom_left

@[simp]
theorem sqFrom_right (f : Hom C‚ÇÅ C‚ÇÇ) (i : Œπ) : (f.sqFrom i).right = f.next i :=
  rfl
#align homological_complex.hom.sq_from_right HomologicalComplex.Hom.sqFrom_right

@[simp]
theorem sqFrom_id (C‚ÇÅ : HomologicalComplex V c) (i : Œπ) : sqFrom (ùüô C‚ÇÅ) i = ùüô _ :=
  rfl
#align homological_complex.hom.sq_from_id HomologicalComplex.Hom.sqFrom_id

@[simp]
theorem sqFrom_comp (f : C‚ÇÅ ‚ü∂ C‚ÇÇ) (g : C‚ÇÇ ‚ü∂ C‚ÇÉ) (i : Œπ) :
    sqFrom (f ‚â´ g) i = sqFrom f i ‚â´ sqFrom g i :=
  rfl
#align homological_complex.hom.sq_from_comp HomologicalComplex.Hom.sqFrom_comp

/-- A morphism of chain complexes
induces a morphism of arrows of the differentials into each object.
-/
def sqTo (f : Hom C‚ÇÅ C‚ÇÇ) (j : Œπ) : Arrow.mk (C‚ÇÅ.dTo j) ‚ü∂ Arrow.mk (C‚ÇÇ.dTo j) :=
  Arrow.homMk (f.comm_to j)
#align homological_complex.hom.sq_to HomologicalComplex.Hom.sqTo

@[simp]
theorem sqTo_left (f : Hom C‚ÇÅ C‚ÇÇ) (j : Œπ) : (f.sqTo j).left = f.prev j :=
  rfl
#align homological_complex.hom.sq_to_left HomologicalComplex.Hom.sqTo_left

@[simp]
theorem sqTo_right (f : Hom C‚ÇÅ C‚ÇÇ) (j : Œπ) : (f.sqTo j).right = f.f j :=
  rfl
#align homological_complex.hom.sq_to_right HomologicalComplex.Hom.sqTo_right

end Hom

end

end HomologicalComplex

namespace ChainComplex

section Of

variable {V} {Œ± : Type*} [AddRightCancelSemigroup Œ±] [One Œ±] [DecidableEq Œ±]

/-- Construct an `Œ±`-indexed chain complex from a dependently-typed differential.
-/
def of (X : Œ± ‚Üí V) (d : ‚àÄ n, X (n + 1) ‚ü∂ X n) (sq : ‚àÄ n, d (n + 1) ‚â´ d n = 0) : ChainComplex V Œ± :=
  { X := X
    d := fun i j => if h : i = j + 1 then eqToHom (by rw [h]) ‚â´ d j else 0
    shape := fun i j w => by
      dsimp
      rw [dif_neg (Ne.symm w)]
    d_comp_d' := fun i j k hij hjk => by
      dsimp at hij hjk
      substs hij hjk
      simp only [eqToHom_refl, id_comp, dite_eq_ite, ite_true, sq] }
#align chain_complex.of ChainComplex.of

variable (X : Œ± ‚Üí V) (d : ‚àÄ n, X (n + 1) ‚ü∂ X n) (sq : ‚àÄ n, d (n + 1) ‚â´ d n = 0)

@[simp]
theorem of_x (n : Œ±) : (of X d sq).X n = X n :=
  rfl
set_option linter.uppercaseLean3 false in
#align chain_complex.of_X ChainComplex.of_x

@[simp]
theorem of_d (j : Œ±) : (of X d sq).d (j + 1) j = d j := by
  dsimp [of]
  rw [if_pos rfl, Category.id_comp]
#align chain_complex.of_d ChainComplex.of_d

theorem of_d_ne {i j : Œ±} (h : i ‚â† j + 1) : (of X d sq).d i j = 0 := by
  dsimp [of]
  rw [dif_neg h]
#align chain_complex.of_d_ne ChainComplex.of_d_ne

end Of

section OfHom

variable {V} {Œ± : Type*} [AddRightCancelSemigroup Œ±] [One Œ±] [DecidableEq Œ±]
variable (X : Œ± ‚Üí V) (d_X : ‚àÄ n, X (n + 1) ‚ü∂ X n) (sq_X : ‚àÄ n, d_X (n + 1) ‚â´ d_X n = 0) (Y : Œ± ‚Üí V)
  (d_Y : ‚àÄ n, Y (n + 1) ‚ü∂ Y n) (sq_Y : ‚àÄ n, d_Y (n + 1) ‚â´ d_Y n = 0)

/-- A constructor for chain maps between `Œ±`-indexed chain complexes built using `ChainComplex.of`,
from a dependently typed collection of morphisms.
-/
@[simps]
def ofHom (f : ‚àÄ i : Œ±, X i ‚ü∂ Y i) (comm : ‚àÄ i : Œ±, f (i + 1) ‚â´ d_Y i = d_X i ‚â´ f i) :
    of X d_X sq_X ‚ü∂ of Y d_Y sq_Y :=
  { f
    comm' := fun n m => by
      by_cases h : n = m + 1
      ¬∑ subst h
        simpa using comm m
      ¬∑ rw [of_d_ne X _ _ h, of_d_ne Y _ _ h]
        simp }
#align chain_complex.of_hom ChainComplex.ofHom

end OfHom

section Mk

variable {V}


variable (X‚ÇÄ X‚ÇÅ X‚ÇÇ : V) (d‚ÇÄ : X‚ÇÅ ‚ü∂ X‚ÇÄ) (d‚ÇÅ : X‚ÇÇ ‚ü∂ X‚ÇÅ) (s : d‚ÇÅ ‚â´ d‚ÇÄ = 0)
  (succ : ‚àÄ (S : ShortComplex V), Œ£' (X‚ÇÉ : V) (d‚ÇÇ : X‚ÇÉ ‚ü∂ S.X‚ÇÅ), d‚ÇÇ ‚â´ S.f = 0)

/-- Auxiliary definition for `mk`. -/
def mkAux : ‚Ñï ‚Üí ShortComplex V
  | 0 => ShortComplex.mk _ _ s
  | n + 1 => ShortComplex.mk _ _ (succ (mkAux n)).2.2
#align chain_complex.mk_aux ChainComplex.mkAux

/-- An inductive constructor for `‚Ñï`-indexed chain complexes.

You provide explicitly the first two differentials,
then a function which takes two differentials and the fact they compose to zero,
and returns the next object, its differential, and the fact it composes appropriately to zero.

See also `mk'`, which only sees the previous differential in the inductive step.
-/
def mk : ChainComplex V ‚Ñï :=
  of (fun n => (mkAux X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ n).X‚ÇÉ) (fun n => (mkAux X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ n).g)
    fun n => (mkAux X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ n).zero
#align chain_complex.mk ChainComplex.mk

@[simp]
theorem mk_X_0 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).X 0 = X‚ÇÄ :=
  rfl
set_option linter.uppercaseLean3 false in
#align chain_complex.mk_X_0 ChainComplex.mk_X_0

@[simp]
theorem mk_X_1 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).X 1 = X‚ÇÅ :=
  rfl
set_option linter.uppercaseLean3 false in
#align chain_complex.mk_X_1 ChainComplex.mk_X_1

@[simp]
theorem mk_X_2 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).X 2 = X‚ÇÇ :=
  rfl
set_option linter.uppercaseLean3 false in
#align chain_complex.mk_X_2 ChainComplex.mk_X_2

@[simp]
theorem mk_d_1_0 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).d 1 0 = d‚ÇÄ := by
  change ite (1 = 0 + 1) (ùüô X‚ÇÅ ‚â´ d‚ÇÄ) 0 = d‚ÇÄ
  rw [if_pos rfl, Category.id_comp]
#align chain_complex.mk_d_1_0 ChainComplex.mk_d_1_0

@[simp]
theorem mk_d_2_1 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).d 2 1 = d‚ÇÅ := by
  change ite (2 = 1 + 1) (ùüô X‚ÇÇ ‚â´ d‚ÇÅ) 0 = d‚ÇÅ
  rw [if_pos rfl, Category.id_comp]
#align chain_complex.mk_d_2_0 ChainComplex.mk_d_2_1

-- TODO simp lemmas for the inductive steps? It's not entirely clear that they are needed.
/-- A simpler inductive constructor for `‚Ñï`-indexed chain complexes.

You provide explicitly the first differential,
then a function which takes a differential,
and returns the next object, its differential, and the fact it composes appropriately to zero.
-/
def mk' (X‚ÇÄ X‚ÇÅ : V) (d : X‚ÇÅ ‚ü∂ X‚ÇÄ)
    (succ' : ‚àÄ {X‚ÇÄ X‚ÇÅ : V} (f : X‚ÇÅ ‚ü∂ X‚ÇÄ), Œ£' (X‚ÇÇ : V) (d : X‚ÇÇ ‚ü∂ X‚ÇÅ), d ‚â´ f = 0) :
    ChainComplex V ‚Ñï :=
  mk _ _ _ _ _ (succ' d).2.2 (fun S => succ' S.f)
#align chain_complex.mk' ChainComplex.mk'

variable (succ' : ‚àÄ {X‚ÇÄ X‚ÇÅ : V} (f : X‚ÇÅ ‚ü∂ X‚ÇÄ), Œ£' (X‚ÇÇ : V) (d : X‚ÇÇ ‚ü∂ X‚ÇÅ), d ‚â´ f = 0)

@[simp]
theorem mk'_X_0 : (mk' X‚ÇÄ X‚ÇÅ d‚ÇÄ succ').X 0 = X‚ÇÄ :=
  rfl
set_option linter.uppercaseLean3 false in
#align chain_complex.mk'_X_0 ChainComplex.mk'_X_0

@[simp]
theorem mk'_X_1 : (mk' X‚ÇÄ X‚ÇÅ d‚ÇÄ succ').X 1 = X‚ÇÅ :=
  rfl
set_option linter.uppercaseLean3 false in
#align chain_complex.mk'_X_1 ChainComplex.mk'_X_1


@[simp]
theorem mk'_d_1_0 : (mk' X‚ÇÄ X‚ÇÅ d‚ÇÄ succ').d 1 0 = d‚ÇÄ := by
  change ite (1 = 0 + 1) (ùüô X‚ÇÅ ‚â´ d‚ÇÄ) 0 = d‚ÇÄ
  rw [if_pos rfl, Category.id_comp]
#align chain_complex.mk'_d_1_0 ChainComplex.mk'_d_1_0

/- Porting note:
Downstream constructions using `mk'` (e.g. in `CategoryTheory.Abelian.Projective`)
have very slow proofs, because of bad simp lemmas.
It would be better to write good lemmas here if possible, such as

```
theorem mk'_X_succ (j : ‚Ñï) :
    (mk' X‚ÇÄ X‚ÇÅ d‚ÇÄ succ').X (j + 2) = (succ' ‚ü®_, _, (mk' X‚ÇÄ X‚ÇÅ d‚ÇÄ succ').d (j + 1) j‚ü©).1 := by
  sorry

theorem mk'_d_succ {i j : ‚Ñï} :
    (mk' X‚ÇÄ X‚ÇÅ d‚ÇÄ succ').d (j + 2) (j + 1) =
      eqToHom (mk'_X_succ X‚ÇÄ X‚ÇÅ d‚ÇÄ succ' j) ‚â´
      (succ' ‚ü®_, _, (mk' X‚ÇÄ X‚ÇÅ d‚ÇÄ succ').d (j + 1) j‚ü©).2.1 :=
  sorry
```

These are already tricky, and it may be better to write analogous lemmas for `mk` first.
-/

end Mk

section MkHom

variable {V}
variable (P Q : ChainComplex V ‚Ñï) (zero : P.X 0 ‚ü∂ Q.X 0) (one : P.X 1 ‚ü∂ Q.X 1)
  (one_zero_comm : one ‚â´ Q.d 1 0 = P.d 1 0 ‚â´ zero)
  (succ :
    ‚àÄ (n : ‚Ñï)
      (p :
        Œ£' (f : P.X n ‚ü∂ Q.X n) (f' : P.X (n + 1) ‚ü∂ Q.X (n + 1)),
          f' ‚â´ Q.d (n + 1) n = P.d (n + 1) n ‚â´ f),
      Œ£'f'' : P.X (n + 2) ‚ü∂ Q.X (n + 2), f'' ‚â´ Q.d (n + 2) (n + 1) = P.d (n + 2) (n + 1) ‚â´ p.2.1)

/-- An auxiliary construction for `mkHom`.

Here we build by induction a family of commutative squares,
but don't require at the type level that these successive commutative squares actually agree.
They do in fact agree, and we then capture that at the type level (i.e. by constructing a chain map)
in `mkHom`.
-/
def mkHomAux :
    ‚àÄ n,
      Œ£' (f : P.X n ‚ü∂ Q.X n) (f' : P.X (n + 1) ‚ü∂ Q.X (n + 1)),
        f' ‚â´ Q.d (n + 1) n = P.d (n + 1) n ‚â´ f
  | 0 => ‚ü®zero, one, one_zero_comm‚ü©
  | n + 1 => ‚ü®(mkHomAux n).2.1, (succ n (mkHomAux n)).1, (succ n (mkHomAux n)).2‚ü©
#align chain_complex.mk_hom_aux ChainComplex.mkHomAux

/-- A constructor for chain maps between `‚Ñï`-indexed chain complexes,
working by induction on commutative squares.

You need to provide the components of the chain map in degrees 0 and 1,
show that these form a commutative square,
and then give a construction of each component,
and the fact that it forms a commutative square with the previous component,
using as an inductive hypothesis the data (and commutativity) of the previous two components.
-/
def mkHom : P ‚ü∂ Q where
  f n := (mkHomAux P Q zero one one_zero_comm succ n).1
  comm' n m := by
    rintro (rfl : m + 1 = n)
    exact (mkHomAux P Q zero one one_zero_comm succ m).2.2
#align chain_complex.mk_hom ChainComplex.mkHom

@[simp]
theorem mkHom_f_0 : (mkHom P Q zero one one_zero_comm succ).f 0 = zero :=
  rfl
#align chain_complex.mk_hom_f_0 ChainComplex.mkHom_f_0

@[simp]
theorem mkHom_f_1 : (mkHom P Q zero one one_zero_comm succ).f 1 = one :=
  rfl
#align chain_complex.mk_hom_f_1 ChainComplex.mkHom_f_1

@[simp]
theorem mkHom_f_succ_succ (n : ‚Ñï) :
    (mkHom P Q zero one one_zero_comm succ).f (n + 2) =
      (succ n
          ‚ü®(mkHom P Q zero one one_zero_comm succ).f n,
            (mkHom P Q zero one one_zero_comm succ).f (n + 1),
            (mkHom P Q zero one one_zero_comm succ).comm (n + 1) n‚ü©).1 := by
  dsimp [mkHom, mkHomAux]
#align chain_complex.mk_hom_f_succ_succ ChainComplex.mkHom_f_succ_succ

end MkHom

end ChainComplex

namespace CochainComplex

section Of

variable {V} {Œ± : Type*} [AddRightCancelSemigroup Œ±] [One Œ±] [DecidableEq Œ±]

/-- Construct an `Œ±`-indexed cochain complex from a dependently-typed differential.
-/
def of (X : Œ± ‚Üí V) (d : ‚àÄ n, X n ‚ü∂ X (n + 1)) (sq : ‚àÄ n, d n ‚â´ d (n + 1) = 0) :
    CochainComplex V Œ± :=
  { X := X
    d := fun i j => if h : i + 1 = j then d _ ‚â´ eqToHom (by rw [h]) else 0
    shape := fun i j w => by
      dsimp
      rw [dif_neg]
      exact w
    d_comp_d' := fun i j k => by
      dsimp
      split_ifs with h h' h'
      ¬∑ substs h h'
        simp [sq]
      all_goals simp }
#align cochain_complex.of CochainComplex.of

variable (X : Œ± ‚Üí V) (d : ‚àÄ n, X n ‚ü∂ X (n + 1)) (sq : ‚àÄ n, d n ‚â´ d (n + 1) = 0)

@[simp]
theorem of_x (n : Œ±) : (of X d sq).X n = X n :=
  rfl
set_option linter.uppercaseLean3 false in
#align cochain_complex.of_X CochainComplex.of_x

@[simp]
theorem of_d (j : Œ±) : (of X d sq).d j (j + 1) = d j := by
  dsimp [of]
  rw [if_pos rfl, Category.comp_id]
#align cochain_complex.of_d CochainComplex.of_d

theorem of_d_ne {i j : Œ±} (h : i + 1 ‚â† j) : (of X d sq).d i j = 0 := by
  dsimp [of]
  rw [dif_neg h]
#align cochain_complex.of_d_ne CochainComplex.of_d_ne

end Of

section OfHom

variable {V} {Œ± : Type*} [AddRightCancelSemigroup Œ±] [One Œ±] [DecidableEq Œ±]
variable (X : Œ± ‚Üí V) (d_X : ‚àÄ n, X n ‚ü∂ X (n + 1)) (sq_X : ‚àÄ n, d_X n ‚â´ d_X (n + 1) = 0) (Y : Œ± ‚Üí V)
  (d_Y : ‚àÄ n, Y n ‚ü∂ Y (n + 1)) (sq_Y : ‚àÄ n, d_Y n ‚â´ d_Y (n + 1) = 0)

/--
A constructor for chain maps between `Œ±`-indexed cochain complexes built using `CochainComplex.of`,
from a dependently typed collection of morphisms.
-/
@[simps]
def ofHom (f : ‚àÄ i : Œ±, X i ‚ü∂ Y i) (comm : ‚àÄ i : Œ±, f i ‚â´ d_Y i = d_X i ‚â´ f (i + 1)) :
    of X d_X sq_X ‚ü∂ of Y d_Y sq_Y :=
  { f
    comm' := fun n m => by
      by_cases h : n + 1 = m
      ¬∑ subst h
        simpa using comm n
      ¬∑ rw [of_d_ne X _ _ h, of_d_ne Y _ _ h]
        simp }
#align cochain_complex.of_hom CochainComplex.ofHom

end OfHom

section Mk

variable {V}
variable (X‚ÇÄ X‚ÇÅ X‚ÇÇ : V) (d‚ÇÄ : X‚ÇÄ ‚ü∂ X‚ÇÅ) (d‚ÇÅ : X‚ÇÅ ‚ü∂ X‚ÇÇ) (s : d‚ÇÄ ‚â´ d‚ÇÅ = 0)
  (succ : ‚àÄ (S : ShortComplex V), Œ£' (X‚ÇÑ : V) (d‚ÇÇ : S.X‚ÇÉ ‚ü∂ X‚ÇÑ), S.g ‚â´ d‚ÇÇ = 0)

/-- Auxiliary definition for `mk`. -/
def mkAux : ‚Ñï ‚Üí ShortComplex V
  | 0 => ShortComplex.mk _ _ s
  | n + 1 => ShortComplex.mk _ _ (succ (mkAux n)).2.2
#align cochain_complex.mk_aux CochainComplex.mkAux

/-- An inductive constructor for `‚Ñï`-indexed cochain complexes.

You provide explicitly the first two differentials,
then a function which takes two differentials and the fact they compose to zero,
and returns the next object, its differential, and the fact it composes appropriately to zero.

See also `mk'`, which only sees the previous differential in the inductive step.
-/
def mk : CochainComplex V ‚Ñï :=
  of (fun n => (mkAux X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ n).X‚ÇÅ) (fun n => (mkAux X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ n).f)
    fun n => (mkAux X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ n).zero
#align cochain_complex.mk CochainComplex.mk

@[simp]
theorem mk_X_0 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).X 0 = X‚ÇÄ :=
  rfl
set_option linter.uppercaseLean3 false in
#align cochain_complex.mk_X_0 CochainComplex.mk_X_0

@[simp]
theorem mk_X_1 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).X 1 = X‚ÇÅ :=
  rfl
set_option linter.uppercaseLean3 false in
#align cochain_complex.mk_X_1 CochainComplex.mk_X_1

@[simp]
theorem mk_X_2 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).X 2 = X‚ÇÇ :=
  rfl
set_option linter.uppercaseLean3 false in
#align cochain_complex.mk_X_2 CochainComplex.mk_X_2

@[simp]
theorem mk_d_1_0 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).d 0 1 = d‚ÇÄ := by
  change ite (1 = 0 + 1) (d‚ÇÄ ‚â´ ùüô X‚ÇÅ) 0 = d‚ÇÄ
  rw [if_pos rfl, Category.comp_id]
#align cochain_complex.mk_d_1_0 CochainComplex.mk_d_1_0

@[simp]
theorem mk_d_2_0 : (mk X‚ÇÄ X‚ÇÅ X‚ÇÇ d‚ÇÄ d‚ÇÅ s succ).d 1 2 = d‚ÇÅ := by
  change ite (2 = 1 + 1) (d‚ÇÅ ‚â´ ùüô X‚ÇÇ) 0 = d‚ÇÅ
  rw [if_pos rfl, Category.comp_id]
#align cochain_complex.mk_d_2_0 CochainComplex.mk_d_2_0

-- TODO simp lemmas for the inductive steps? It's not entirely clear that they are needed.
/-- A simpler inductive constructor for `‚Ñï`-indexed cochain complexes.

You provide explicitly the first differential,
then a function which takes a differential,
and returns the next object, its differential, and the fact it composes appropriately to zero.
-/
def mk' (X‚ÇÄ X‚ÇÅ : V) (d : X‚ÇÄ ‚ü∂ X‚ÇÅ)
    -- (succ' : ‚àÄ  : Œ£X‚ÇÄ X‚ÇÅ : V, X‚ÇÄ ‚ü∂ X‚ÇÅ, Œ£' (X‚ÇÇ : V) (d : t.2.1 ‚ü∂ X‚ÇÇ), t.2.2 ‚â´ d = 0) :
    (succ' : ‚àÄ {X‚ÇÄ X‚ÇÅ : V} (f : X‚ÇÄ ‚ü∂ X‚ÇÅ), Œ£' (X‚ÇÇ : V) (d : X‚ÇÅ ‚ü∂ X‚ÇÇ), f ‚â´ d = 0) :
    CochainComplex V ‚Ñï :=
  mk _ _ _ _ _ (succ' d).2.2 (fun S => succ' S.g)
#align cochain_complex.mk' CochainComplex.mk'

variable (succ' : ‚àÄ {X‚ÇÄ X‚ÇÅ : V} (f : X‚ÇÄ ‚ü∂ X‚ÇÅ), Œ£' (X‚ÇÇ : V) (d : X‚ÇÅ ‚ü∂ X‚ÇÇ), f ‚â´ d = 0)

@[simp]
theorem mk'_X_0 : (mk' X‚ÇÄ X‚ÇÅ d‚ÇÄ succ').X 0 = X‚ÇÄ :=
  rfl
set_option linter.uppercaseLean3 false in
#align cochain_complex.mk'_X_0 CochainComplex.mk'_X_0

@[simp]
theorem mk'_X_1 : (mk' X‚ÇÄ X‚ÇÅ d‚ÇÄ succ').X 1 = X‚ÇÅ :=
  rfl
set_option linter.uppercaseLean3 false in
#align cochain_complex.mk'_X_1 CochainComplex.mk'_X_1

@[simp]
theorem mk'_d_1_0 : (mk' X‚ÇÄ X‚ÇÅ d‚ÇÄ succ').d 0 1 = d‚ÇÄ := by
  change ite (1 = 0 + 1) (d‚ÇÄ ‚â´ ùüô X‚ÇÅ) 0 = d‚ÇÄ
  rw [if_pos rfl, Category.comp_id]
#align cochain_complex.mk'_d_1_0 CochainComplex.mk'_d_1_0

-- TODO simp lemmas for the inductive steps? It's not entirely clear that they are needed.
end Mk

section MkHom

variable {V}
variable (P Q : CochainComplex V ‚Ñï) (zero : P.X 0 ‚ü∂ Q.X 0) (one : P.X 1 ‚ü∂ Q.X 1)
  (one_zero_comm : zero ‚â´ Q.d 0 1 = P.d 0 1 ‚â´ one)
  (succ : ‚àÄ (n : ‚Ñï) (p : Œ£' (f : P.X n ‚ü∂ Q.X n) (f' : P.X (n + 1) ‚ü∂ Q.X (n + 1)),
          f ‚â´ Q.d n (n + 1) = P.d n (n + 1) ‚â´ f'),
      Œ£' f'' : P.X (n + 2) ‚ü∂ Q.X (n + 2), p.2.1 ‚â´ Q.d (n + 1) (n + 2) = P.d (n + 1) (n + 2) ‚â´ f'')

/-- An auxiliary construction for `mkHom`.

Here we build by induction a family of commutative squares,
but don't require at the type level that these successive commutative squares actually agree.
They do in fact agree, and we then capture that at the type level (i.e. by constructing a chain map)
in `mkHom`.
-/
def mkHomAux :
    ‚àÄ n,
      Œ£' (f : P.X n ‚ü∂ Q.X n) (f' : P.X (n + 1) ‚ü∂ Q.X (n + 1)),
        f ‚â´ Q.d n (n + 1) = P.d n (n + 1) ‚â´ f'
  | 0 => ‚ü®zero, one, one_zero_comm‚ü©
  | n + 1 => ‚ü®(mkHomAux n).2.1, (succ n (mkHomAux n)).1, (succ n (mkHomAux n)).2‚ü©
#align cochain_complex.mk_hom_aux CochainComplex.mkHomAux

/-- A constructor for chain maps between `‚Ñï`-indexed cochain complexes,
working by induction on commutative squares.

You need to provide the components of the chain map in degrees 0 and 1,
show that these form a commutative square,
and then give a construction of each component,
and the fact that it forms a commutative square with the previous component,
using as an inductive hypothesis the data (and commutativity) of the previous two components.
-/
def mkHom : P ‚ü∂ Q where
  f n := (mkHomAux P Q zero one one_zero_comm succ n).1
  comm' n m := by
    rintro (rfl : n + 1 = m)
    exact (mkHomAux P Q zero one one_zero_comm succ n).2.2
#align cochain_complex.mk_hom CochainComplex.mkHom

@[simp]
theorem mkHom_f_0 : (mkHom P Q zero one one_zero_comm succ).f 0 = zero :=
  rfl
#align cochain_complex.mk_hom_f_0 CochainComplex.mkHom_f_0

@[simp]
theorem mkHom_f_1 : (mkHom P Q zero one one_zero_comm succ).f 1 = one :=
  rfl
#align cochain_complex.mk_hom_f_1 CochainComplex.mkHom_f_1

@[simp]
theorem mkHom_f_succ_succ (n : ‚Ñï) :
    (mkHom P Q zero one one_zero_comm succ).f (n + 2) =
      (succ n
          ‚ü®(mkHom P Q zero one one_zero_comm succ).f n,
            (mkHom P Q zero one one_zero_comm succ).f (n + 1),
            (mkHom P Q zero one one_zero_comm succ).comm n (n + 1)‚ü©).1 := by
  dsimp [mkHom, mkHomAux]
#align cochain_complex.mk_hom_f_succ_succ CochainComplex.mkHom_f_succ_succ

end MkHom

end CochainComplex
