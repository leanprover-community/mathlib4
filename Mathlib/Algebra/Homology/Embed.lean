import Mathlib.Algebra.Homology.ShortComplex.HomologicalComplex

-- mostly from LTE

open CategoryTheory Limits ZeroObject Category

namespace CategoryTheory

variable {C : Type*} [Category C] [HasZeroMorphisms C]

namespace Limits

namespace KernelFork

variable {X Y : C} {g : X ‚ü∂ Y} (c : KernelFork g) (hc : IsLimit c)

def isLimitOfIsLimitOfIff {Y' : C} (g' : X ‚ü∂ Y')
    (iff : ‚àÄ ‚¶ÉW : C‚¶Ñ (œÜ : W ‚ü∂ X), œÜ ‚â´ g = 0 ‚Üî œÜ ‚â´ g' = 0) :
    IsLimit (KernelFork.ofŒπ (f := g') c.Œπ (by rw [‚Üê iff, c.condition])) :=
  KernelFork.IsLimit.ofŒπ _ _
    (fun s hs => hc.lift (KernelFork.ofŒπ s (by rw [iff, hs])))
    (fun s hs => hc.fac _ _)
    (fun s hs m hm => Fork.IsLimit.hom_ext hc (by simp [hm]))

def isLimitOfIsLimitOfIff' {X' Y' : C} (g' : X' ‚ü∂ Y') (e : X ‚âÖ X')
    (iff : ‚àÄ ‚¶ÉW : C‚¶Ñ (œÜ : W ‚ü∂ X), œÜ ‚â´ g = 0 ‚Üî œÜ ‚â´ e.hom ‚â´ g' = 0) :
    IsLimit (KernelFork.ofŒπ (f := g') (c.Œπ ‚â´ e.hom) (by simp [‚Üê iff])) := by
  let e' : parallelPair g' 0 ‚âÖ parallelPair (e.hom ‚â´ g') 0 :=
    parallelPair.ext e.symm (Iso.refl _) (by simp) (by simp)
  refine (IsLimit.postcomposeHomEquiv e' _).1
    (IsLimit.ofIsoLimit (isLimitOfIsLimitOfIff c hc (e.hom ‚â´ g') iff)
      (Fork.ext (Iso.refl _) ?_))
  change ùüô _ ‚â´ (c.Œπ ‚â´ e.hom) ‚â´ e.inv = c.Œπ
  simp

end KernelFork

namespace CokernelCofork

variable {X Y : C} {f : X ‚ü∂ Y} (c : CokernelCofork f) (hc : IsColimit c)

def isColimitOfIsColimitOfIff {X' : C} (f' : X' ‚ü∂ Y)
    (iff : ‚àÄ ‚¶ÉW : C‚¶Ñ (œÜ : Y ‚ü∂ W), f ‚â´ œÜ = 0 ‚Üî f' ‚â´ œÜ = 0) :
    IsColimit (CokernelCofork.ofœÄ (f := f') c.œÄ (by rw [‚Üê iff, c.condition])) :=
  CokernelCofork.IsColimit.ofœÄ _ _
    (fun s hs => hc.desc (CokernelCofork.ofœÄ s (by rw [iff, hs])))
    (fun s hs => hc.fac _ _)
    (fun s hs m hm => Cofork.IsColimit.hom_ext hc (by simp [hm]))

def isColimitOfIsColimitOfIff' {X' Y' : C} (f' : X' ‚ü∂ Y') (e : Y' ‚âÖ Y)
    (iff : ‚àÄ ‚¶ÉW : C‚¶Ñ (œÜ : Y ‚ü∂ W), f ‚â´ œÜ = 0 ‚Üî f' ‚â´ e.hom ‚â´ œÜ = 0) :
    IsColimit (CokernelCofork.ofœÄ (f := f') (e.hom ‚â´ c.œÄ) (by simp [‚Üê iff])) := by
  let e' : parallelPair (f' ‚â´ e.hom) 0 ‚âÖ parallelPair f' 0 :=
    parallelPair.ext (Iso.refl _) e.symm (by simp) (by simp)
  refine (IsColimit.precomposeHomEquiv e' _).1
    (IsColimit.ofIsoColimit (isColimitOfIsColimitOfIff c hc (f' ‚â´ e.hom)
      (by simpa only [assoc] using iff)) (Cofork.ext (Iso.refl _) ?_))
  change c.œÄ ‚â´ ùüô _ = e.inv ‚â´ e.hom ‚â´ c.œÄ
  simp

end CokernelCofork

end Limits

end CategoryTheory

variable {Œπ Œπ' : Type*} (c : ComplexShape Œπ) (c' : ComplexShape Œπ')

lemma Option.eq_none_or_eq_some (x : Option Œπ) :
    x = none ‚à® ‚àÉ y, x = some y := by
  cases x
  ¬∑ exact Or.inl rfl
  ¬∑ exact Or.inr ‚ü®_, rfl‚ü©

namespace ComplexShape

structure Embed where
  f : Œπ ‚Üí Œπ'
  injective_f : Function.Injective f
  rel {i‚ÇÅ i‚ÇÇ : Œπ} (h : c.Rel i‚ÇÅ i‚ÇÇ) : c'.Rel (f i‚ÇÅ) (f i‚ÇÇ)

namespace Embed

variable {c c'}
variable (e : Embed c c')

open Classical in
noncomputable def r (i' : Œπ') : Option Œπ :=
  if h : ‚àÉ (i : Œπ), e.f i = i'
  then some h.choose
  else none

lemma r_eq_some {i : Œπ} {i' : Œπ'} (hi : e.f i = i') :
    e.r i' = some i := by
  have h : ‚àÉ (i : Œπ), e.f i = i' := ‚ü®i, hi‚ü©
  have : h.choose = i := e.injective_f (h.choose_spec.trans (hi.symm))
  dsimp [r]
  rw [dif_pos ‚ü®i, hi‚ü©, this]

lemma f_eq_of_r_eq_some {i : Œπ} {i' : Œπ'} (hi : e.r i' = some i) :
    e.f i = i' := by
  by_cases h : ‚àÉ (k : Œπ), e.f k = i'
  ¬∑ obtain ‚ü®k, hk‚ü© := h
    have : some i = some k := by
      rw [‚Üê e.r_eq_some hk, hi]
    rw [‚Üê hk]
    congr 1
    simpa using this
  ¬∑ simp [r, dif_neg h] at hi

end Embed

end ComplexShape

namespace HomologicalComplex

variable {c c'} {C : Type*} [Category C] [HasZeroMorphisms C]
  [HasZeroObject C]

variable (K : HomologicalComplex C c) (e : c.Embed c')

namespace extend

noncomputable def X : Option Œπ ‚Üí C
  | some x => K.X x
  | none => 0

noncomputable def XIso {i : Option Œπ} {j : Œπ} (hj : i = some j) :
    X K i ‚âÖ K.X j := eqToIso (by subst hj; rfl)

lemma isZero_X {i : Option Œπ} (hi : i = none) :
    IsZero (X K i) := by
  subst hi
  exact Limits.isZero_zero _

noncomputable def d : ‚àÄ (i j : Option Œπ), extend.X K i ‚ü∂ extend.X K j
  | none, _ => 0
  | some i, some j => K.d i j
  | some _, none => 0

lemma d_none_eq_zero (i j : Option Œπ) (hi : i = none) :
    d K i j = 0 := by subst hi; rfl

lemma d_none_eq_zero' (i j : Option Œπ) (hj : j = none) :
    d K i j = 0 := by subst hj; cases i <;> rfl

lemma d_eq {i j : Option Œπ} {a b : Œπ}
    (hi : i = some a) (hj : j = some b) :
    d K i j = (XIso K hi).hom ‚â´ K.d a b ‚â´ (XIso K hj).inv := by
  subst hi hj
  dsimp [XIso, d]
  erw [id_comp, comp_id]

end extend

noncomputable def extend : HomologicalComplex C c' where
  X i' := extend.X K (e.r i')
  d i' j' := extend.d K (e.r i') (e.r j')
  shape i' j' h := by
    dsimp
    obtain hi'|‚ü®i, hi‚ü© := (e.r i').eq_none_or_eq_some
    ¬∑ rw [extend.d_none_eq_zero K _ _ hi']
    ¬∑ obtain hj'|‚ü®j, hj‚ü© := (e.r j').eq_none_or_eq_some
      ¬∑ rw [extend.d_none_eq_zero' K _ _ hj']
      ¬∑ rw [extend.d_eq K hi hj,K.shape, zero_comp, comp_zero]
        obtain rfl := e.f_eq_of_r_eq_some hi
        obtain rfl := e.f_eq_of_r_eq_some hj
        intro hij
        exact h (e.rel hij)
  d_comp_d' i' j' k' _ _ := by
    dsimp
    obtain hi'|‚ü®i, hi‚ü© := (e.r i').eq_none_or_eq_some
    ¬∑ rw [extend.d_none_eq_zero K _ _ hi', zero_comp]
    ¬∑ obtain hj'|‚ü®j, hj‚ü© := (e.r j').eq_none_or_eq_some
      ¬∑ rw [extend.d_none_eq_zero K _ _ hj', comp_zero]
      ¬∑ obtain hk'|‚ü®k, hk‚ü© := (e.r k').eq_none_or_eq_some
        ¬∑ rw [extend.d_none_eq_zero' K _ _ hk', comp_zero]
        ¬∑ rw [extend.d_eq K hi hj,
            extend.d_eq K hj hk, assoc, assoc,
            Iso.inv_hom_id_assoc, K.d_comp_d_assoc, zero_comp, comp_zero]

noncomputable def extendXIso {i' : Œπ'} {i : Œπ} (h : e.f i = i') :
    (K.extend e).X i' ‚âÖ K.X i :=
  extend.XIso K (e.r_eq_some h)

lemma isZero_extend_X (i' : Œπ') (hi' : e.r i' = none) :
    IsZero ((K.extend e).X i') :=
  extend.isZero_X K hi'

lemma extend_d_eq {i' j' : Œπ'} {i j : Œπ} (hi : e.f i = i') (hj : e.f j = j') :
    (K.extend e).d i' j' = (K.extendXIso e hi).hom ‚â´ K.d i j ‚â´
      (K.extendXIso e hj).inv := by
  apply extend.d_eq

lemma extend_d_from_eq_zero (i' j' : Œπ') (i : Œπ) (hi : e.f i = i') (hi' : ¬¨ c.Rel i (c.next i)) :
    (K.extend e).d i' j' = 0 := by
  obtain hj'|‚ü®j, hj‚ü© := (e.r j').eq_none_or_eq_some
  ¬∑ exact extend.d_none_eq_zero' _ _ _ hj'
  ¬∑ rw [extend_d_eq K e hi (e.f_eq_of_r_eq_some hj), K.shape, zero_comp, comp_zero]
    intro hij
    obtain rfl := c.next_eq' hij
    exact hi' hij

lemma extend_d_to_eq_zero (i' j' : Œπ') (j : Œπ) (hj : e.f j = j') (hj' : ¬¨ c.Rel (c.prev j) j) :
    (K.extend e).d i' j' = 0 := by
  obtain hi'|‚ü®i, hi‚ü© := (e.r i').eq_none_or_eq_some
  ¬∑ exact extend.d_none_eq_zero _ _ _ hi'
  ¬∑ rw [extend_d_eq K e (e.f_eq_of_r_eq_some hi) hj, K.shape, zero_comp, comp_zero]
    intro hij
    obtain rfl := c.prev_eq' hij
    exact hj' hij

namespace extend

section LeftHomologyData

variable {i j k : Œπ} {i' j' k' : Œπ'} (hj' : e.f j = j')
  (hi : c.prev j = i) (hi' : c'.prev j' = i') (hk : c.next j = k) (hk' : c'.next j' = k')

lemma comp_d_eq_zero_iff ‚¶ÉW : C‚¶Ñ (œÜ : W ‚ü∂ K.X j) :
    œÜ ‚â´ K.d j k = 0 ‚Üî œÜ ‚â´ (K.extendXIso e hj').inv ‚â´ (K.extend e).d j' k' = 0 := by
  by_cases hjk : c.Rel j k
  ¬∑ have hk' : e.f k = k' := by rw [‚Üê hk', ‚Üê hj', c'.next_eq' (e.rel hjk)]
    rw [K.extend_d_eq e hj' hk', Iso.inv_hom_id_assoc,
      ‚Üê cancel_mono (K.extendXIso e hk').inv, zero_comp, assoc]
  ¬∑ simp only [K.shape _ _ hjk, comp_zero, true_iff]
    rw [K.extend_d_from_eq_zero e j' k' j hj', comp_zero, comp_zero]
    rw [hk]
    exact hjk

namespace LeftHomologyData

variable (cone : KernelFork (K.d j k)) (hcone : IsLimit cone)

lemma kernelFork_condition :
    (Fork.Œπ cone ‚â´ (extendXIso K e hj').inv) ‚â´ (extend K e).d j' k' = 0 := by
  rw [assoc, ‚Üê comp_d_eq_zero_iff K e hj' hk hk' cone.Œπ, cone.condition]

@[simp]
noncomputable def kernelFork : KernelFork ((K.extend e).d j' k') :=
  KernelFork.ofŒπ (Fork.Œπ cone ‚â´ (extendXIso K e hj').inv)
    (by rw [assoc, ‚Üê comp_d_eq_zero_iff K e hj' hk hk' cone.Œπ, cone.condition])

noncomputable def isLimitKernelFork : IsLimit (kernelFork K e hj' hk hk' cone) :=
  KernelFork.isLimitOfIsLimitOfIff' cone hcone ((K.extend e).d j' k')
    (extendXIso K e hj').symm (comp_d_eq_zero_iff K e hj' hk hk')

variable (cocone : CokernelCofork (hcone.lift (KernelFork.ofŒπ (K.d i j) (K.d_comp_d i j k))))
  (hcocone : IsColimit cocone)

lemma lift_d_comp_eq_zero_iff ‚¶ÉW : C‚¶Ñ (œÜ : cone.pt ‚ü∂ W) :
    hcone.lift (KernelFork.ofŒπ (K.d i j) (K.d_comp_d i j k)) ‚â´ œÜ = 0 ‚Üî
      ((isLimitKernelFork K e hj' hk hk' cone hcone).lift
      (KernelFork.ofŒπ ((K.extend e).d i' j') (d_comp_d _ _ _ _))) ‚â´ œÜ = 0 := by
  have ‚ü®f', h‚ÇÅ‚ü© : ‚àÉ f', f' = hcone.lift (KernelFork.ofŒπ (K.d i j) (K.d_comp_d i j k)) := ‚ü®_, rfl‚ü©
  have ‚ü®f'', h‚ÇÇ‚ü© : ‚àÉ f'', f'' = ((isLimitKernelFork K e hj' hk hk' cone hcone).lift
      (KernelFork.ofŒπ ((K.extend e).d i' j') (d_comp_d _ _ _ _))) := ‚ü®_, rfl‚ü©
  suffices f' ‚â´ œÜ = 0 ‚Üî f'' ‚â´ œÜ = 0 by simpa only [h‚ÇÅ, h‚ÇÇ] using this
  have hf' : f' ‚â´ cone.Œπ = K.d i j := by rw [h‚ÇÅ]; apply hcone.fac _
  have hf'' : f'' ‚â´ cone.Œπ = (K.extend e).d i' j' ‚â´ (extendXIso K e hj').hom := by
    rw [h‚ÇÇ, ‚Üê cancel_mono (extendXIso K e hj').inv, assoc, assoc, Iso.hom_inv_id, comp_id]
    exact ((isLimitKernelFork K e hj' hk hk' cone hcone).fac _) WalkingParallelPair.zero
  by_cases hij : c.Rel i j
  ¬∑ have hi'' : e.f i = i' := by rw [‚Üê hi', ‚Üê hj', c'.prev_eq' (e.rel hij)]
    have : (K.extendXIso e hi'').hom ‚â´ f' = f'' := by
      apply Fork.IsLimit.hom_ext hcone
      rw [assoc, hf', hf'', K.extend_d_eq e hi'' hj', assoc, assoc, Iso.inv_hom_id, comp_id]
    rw [‚Üê cancel_epi (K.extendXIso e hi'').hom, comp_zero, ‚Üê this, assoc]
  ¬∑ have h‚ÇÉ : f' = 0 := by
      apply Fork.IsLimit.hom_ext hcone
      simp only [zero_comp, hf', K.shape _ _ hij]
    have h‚ÇÑ : f'' = 0 := by
      apply Fork.IsLimit.hom_ext hcone
      dsimp
      rw [hf'', zero_comp, K.extend_d_to_eq_zero e i' j' j hj', zero_comp]
      rw [hi]
      exact hij
    simp [h‚ÇÉ, h‚ÇÑ]

noncomputable def cokernelCofork :
    CokernelCofork ((isLimitKernelFork K e hj' hk hk' cone hcone).lift
      (KernelFork.ofŒπ ((K.extend e).d i' j') (d_comp_d _ _ _ _))) :=
  CokernelCofork.ofœÄ cocone.œÄ (by
    rw [‚Üê lift_d_comp_eq_zero_iff K e hj' hi hi' hk hk' cone hcone]
    exact cocone.condition)

noncomputable def isColimitCokernelCofork :
    IsColimit (cokernelCofork K e hj' hi hi' hk hk' cone hcone cocone) :=
  CokernelCofork.isColimitOfIsColimitOfIff cocone hcocone _
    (lift_d_comp_eq_zero_iff K e hj' hi hi' hk hk' cone hcone)

end LeftHomologyData

variable (h : (K.sc' i j k).LeftHomologyData)

noncomputable def leftHomologyData  :
    ((K.extend e).sc' i' j' k').LeftHomologyData where
  K := h.K
  H := h.H
  i := h.i ‚â´ (extendXIso K e hj').inv
  œÄ := h.œÄ
  wi := by
    dsimp
    rw [assoc, ‚Üê comp_d_eq_zero_iff K e hj' hk hk']
    exact h.wi
  hi := LeftHomologyData.isLimitKernelFork K e hj' hk hk' _ h.hi
  wœÄ := by
    erw [‚Üê LeftHomologyData.lift_d_comp_eq_zero_iff K e hj' hi hi' hk hk' _ h.hi]
    exact h.wœÄ
  hœÄ := LeftHomologyData.isColimitCokernelCofork K e hj' hi hi' hk hk' _ h.hi _ h.hœÄ

end LeftHomologyData

end extend

end HomologicalComplex
