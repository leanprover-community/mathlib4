/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.Algebra.Homology.Embedding.RestrictionHomology

/-!
# Connecting a chain complex and a cochain complex

Given a chain complex `K`: `... ‚ü∂ K.X 2 ‚ü∂ K.X 1 ‚ü∂ K.X 0`,
a cochain complex `L`: `L.X 0 ‚ü∂ L.X 1 ‚ü∂ L.X 2 ‚ü∂ ...`,
a morphism `d‚ÇÄ : K.X 0 ‚ü∂ L.X 0` satisfying the identifies `K.d 1 0 ‚â´ d‚ÇÄ = 0`
and `d‚ÇÄ ‚â´ L.d 0 1 = 0`, we construct a cochain complex indexed by `‚Ñ§` of the form
`... ‚ü∂ K.X 2 ‚ü∂ K.X 1 ‚ü∂ K.X 0 ‚ü∂ L.X 0 ‚ü∂ L.X 1 ‚ü∂ L.X 2 ‚ü∂ ...`,
where `K.X 0` lies in degree `-1` and `L.X 0` in degree `0`.

## Main definitions

Say `K : ChainComplex C ‚Ñï` and `L : CochainComplex C ‚Ñï`, so `... ‚ü∂ K‚ÇÇ ‚ü∂ K‚ÇÅ ‚ü∂ K‚ÇÄ`
and `L‚Å∞ ‚ü∂ L¬π ‚ü∂ L¬≤ ‚ü∂ ...`.

* `ConnectData K L`: an auxiliary structure consisting of `d‚ÇÄ : K‚ÇÄ ‚ü∂ L‚Å∞` "connecting" the
  complexes and proofs that the induced maps `K‚ÇÅ ‚ü∂ K‚ÇÄ ‚ü∂ L‚Å∞` and `K‚ÇÄ ‚ü∂ L‚Å∞ ‚ü∂ L¬π` are both zero.

Now say `h : ConnectData K L`.

* `CochainComplex.ConnectData.cochainComplex h` : the induced ‚Ñ§-indexed complex
  `... ‚ü∂ K‚ÇÅ ‚ü∂ K‚ÇÄ ‚ü∂ L‚Å∞ ‚ü∂ L¬π ‚ü∂ ...`
* `CochainComplex.ConnectData.homologyIsoPos h (n : ‚Ñï) (m : ‚Ñ§)` : if `m = n + 1`,
  the isomorphism `h.cochainComplex.homology m ‚âÖ L.homology (n + 1)`
* `CochainComplex.ConnectData.homologyIsoNeg h (n : ‚Ñï) (m : ‚Ñ§)` : if `m = -(n + 2)`,
  the isomorphism `h.cochainComplex.homology m ‚âÖ K.homology (n + 1)`

## TODO

* Computation of `h.cochainComplex.homology k` when `k = 0` or `k = -1`.

-/

@[expose] public section

universe v u

open CategoryTheory Limits

variable {C : Type u} [Category.{v} C] [HasZeroMorphisms C]

namespace CochainComplex

variable {K K' K'' : ChainComplex C ‚Ñï} {L L' L'' : CochainComplex C ‚Ñï}

variable (K L) in
/-- Given `K : ChainComplex C ‚Ñï` and `L : CochainComplex C ‚Ñï`, this data
allows to connect `K` and `L` in order to get a cochain complex indexed by `‚Ñ§`,
see `ConnectData.cochainComplex`. -/
structure ConnectData where
  /-- the differential which connect `K` and `L` -/
  d‚ÇÄ : K.X 0 ‚ü∂ L.X 0
  comp_d‚ÇÄ : K.d 1 0 ‚â´ d‚ÇÄ = 0
  d‚ÇÄ_comp : d‚ÇÄ ‚â´ L.d 0 1 = 0

namespace ConnectData

attribute [reassoc (attr := simp)] comp_d‚ÇÄ d‚ÇÄ_comp

variable (h : ConnectData K L) (h' : ConnectData K' L') (h'' : ConnectData K'' L'')

variable (K L) in
/-- Auxiliary definition for `ConnectData.cochainComplex`. -/
def X : ‚Ñ§ ‚Üí C
  | .ofNat n => L.X n
  | .negSucc n => K.X n

@[simp] lemma X_ofNat (n : ‚Ñï) : X K L n = L.X n := rfl
@[simp] lemma X_negSucc (n : ‚Ñï) : X K L (.negSucc n) = K.X n := rfl
@[simp] lemma X_zero : X K L 0 = L.X 0 := rfl
@[simp] lemma X_negOne : X K L (-1) = K.X 0 := rfl

/-- Auxiliary definition for `ConnectData.cochainComplex`. -/
def d : ‚àÄ (n m : ‚Ñ§), X K L n ‚ü∂ X K L m
  | .ofNat n, .ofNat m => L.d n m
  | .negSucc n, .negSucc m => K.d n m
  | .negSucc 0, .ofNat 0 => h.d‚ÇÄ
  | .ofNat _, .negSucc _ => 0
  | .negSucc _, .ofNat _ => 0

@[simp] lemma d_ofNat (n m : ‚Ñï) : h.d n m = L.d n m := rfl
@[simp] lemma d_negSucc (n m : ‚Ñï) : h.d (.negSucc n) (.negSucc m) = K.d n m := by simp [d]
@[simp] lemma d_sub_one_zero : h.d (-1) 0 = h.d‚ÇÄ := rfl
@[simp] lemma d_zero_one : h.d 0 1 = L.d 0 1 := rfl
@[simp] lemma d_sub_two_sub_one : h.d (-2) (-1) = K.d 1 0 := rfl

lemma shape (n m : ‚Ñ§) (hnm : n + 1 ‚â† m) : h.d n m = 0 :=
  match n, m with
  | .ofNat n, .ofNat m => L.shape _ _ (by simp at hnm ‚ä¢; cutsat)
  | .negSucc n, .negSucc m => by
    simpa only [d_negSucc] using K.shape n m (by simp at hnm ‚ä¢; cutsat)
  | .negSucc 0, .ofNat 0 => by simp at hnm
  | .ofNat _, .negSucc m => rfl
  | .negSucc n, .ofNat m => by
    obtain _ | n := n
    ¬∑ obtain _ | m := m
      ¬∑ simp at hnm
      ¬∑ rfl
    ¬∑ simp only [d]

@[reassoc (attr := simp)]
lemma d_comp_d (n m p : ‚Ñ§) : h.d n m ‚â´ h.d m p = 0 := by
  by_cases hnm : n + 1 = m; swap
  ¬∑ rw [h.shape n m hnm, zero_comp]
  by_cases hmp : m + 1 = p; swap
  ¬∑ rw [h.shape m p hmp, comp_zero]
  obtain n | (_ | _ | n) := n
  ¬∑ obtain rfl : m = .ofNat (n + 1) := by simp [‚Üê hnm]
    obtain rfl : p = .ofNat (n + 2) := by simp [‚Üê hmp]; cutsat
    simp only [Int.ofNat_eq_natCast, X_ofNat, d_ofNat, HomologicalComplex.d_comp_d]
  ¬∑ obtain rfl : m = 0 := by cutsat
    obtain rfl : p = 1 := by cutsat
    simp
  ¬∑ obtain rfl : m = -1 := by cutsat
    obtain rfl : p = 0 := by cutsat
    simp
  ¬∑ obtain rfl : m = .negSucc (n + 1) := by cutsat
    obtain rfl : p = .negSucc n := by cutsat
    simp

/-- Given `h : ConnectData K L` where `K : ChainComplex C ‚Ñï` and `L : CochainComplex C ‚Ñï`,
this is the cochain complex indexed by `‚Ñ§` obtained by connecting `K` and `L`:
`... ‚ü∂ K.X 2 ‚ü∂ K.X 1 ‚ü∂ K.X 0 ‚ü∂ L.X 0 ‚ü∂ L.X 1 ‚ü∂ L.X 2 ‚ü∂ ...`. -/
@[simps]
def cochainComplex : CochainComplex C ‚Ñ§ where
  X := X K L
  d := h.d
  shape := h.shape

open HomologicalComplex

/-- If `h : ConnectData K L`, then `h.cochainComplex` identifies to `L` in degrees `‚â• 0`. -/
@[simps!]
def restrictionGEIso :
    h.cochainComplex.restriction (ComplexShape.embeddingUpIntGE 0) ‚âÖ L :=
  Hom.isoOfComponents
    (fun n ‚Ü¶ h.cochainComplex.restrictionXIso (ComplexShape.embeddingUpIntGE 0)
      (i := n) (i' := n) (by simp)) (by
    rintro n _ rfl
    dsimp only
    rw [restriction_d_eq (e := (ComplexShape.embeddingUpIntGE 0)) _ (i' := n)
      (j' := (n + 1 : ‚Ñï)) (by simp) (by simp), cochainComplex_d, h.d_ofNat]
    simp)

/-- If `h : ConnectData K L`, then `h.cochainComplex` identifies to `K` in degrees `‚â§ -1`. -/
@[simps!]
def restrictionLEIso :
    h.cochainComplex.restriction (ComplexShape.embeddingUpIntLE (-1)) ‚âÖ K :=
  Hom.isoOfComponents
    (fun n ‚Ü¶ h.cochainComplex.restrictionXIso (ComplexShape.embeddingUpIntLE (-1))
        (i := n) (i' := .negSucc n) (by dsimp; cutsat)) (by
    rintro _ n rfl
    dsimp only
    rw [restriction_d_eq (e := (ComplexShape.embeddingUpIntLE (-1))) _
      (i' := Int.negSucc (n + 1)) (j' := Int.negSucc n) (by dsimp; cutsat) (by dsimp; cutsat),
      cochainComplex_d, d_negSucc]
    simp)

/-- Given `h : ConnectData K L` and `n : ‚Ñï` non-zero, the homology
of `h.cochainComplex` in degree `n` identifies to the homology of `L` in degree `n`. -/
noncomputable def homologyIsoPos (n : ‚Ñï) [NeZero n] (m : ‚Ñ§) (hm : m = n)
    [h.cochainComplex.HasHomology m] [L.HasHomology n] :
    h.cochainComplex.homology m ‚âÖ L.homology n :=
  have := hasHomology_of_iso h.restrictionGEIso.symm n
  (h.cochainComplex.restrictionHomologyIso
    (ComplexShape.embeddingUpIntGE 0) (n - 1) n (n + 1) (by cases n <;> simp) (by simp)
      (i' := m - 1) (j' := m) (k' := m + 1) (by have := NeZero.ne n; cases n <;> simp <;> cutsat)
      (by simp; cutsat) (by simp; cutsat) (by simp) (by simp)).symm ‚â™‚â´
    HomologicalComplex.homologyMapIso h.restrictionGEIso n

/-- Given `h : ConnectData K L` and `n : ‚Ñï` non-zero, the homology
of `h.cochainComplex` in degree `-(n + 1)` identifies to the homology of `K` in degree `n`. -/
noncomputable def homologyIsoNeg (n : ‚Ñï) [NeZero n] (m : ‚Ñ§) (hm : m = -(n + 1 : ‚Ñï))
    [h.cochainComplex.HasHomology m] [K.HasHomology n] :
    h.cochainComplex.homology m ‚âÖ K.homology n :=
  have := hasHomology_of_iso h.restrictionLEIso.symm n
  (h.cochainComplex.restrictionHomologyIso
    (ComplexShape.embeddingUpIntLE (-1)) (n + 1) n (n - 1) (by simp) (by cases n <;> simp)
      (i' := m - 1) (j' := m) (k' := m + 1) (by simp; cutsat) (by simp; cutsat)
      (by have := NeZero.ne n; cases n <;> simp <;> cutsat) (by simp) (by simp)).symm ‚â™‚â´
    HomologicalComplex.homologyMapIso h.restrictionLEIso n

variable
  (fK : K ‚ü∂ K') (fL : L ‚ü∂ L') (f_comm : fK.f 0 ‚â´ h'.d‚ÇÄ = h.d‚ÇÄ ‚â´ fL.f 0)
  (fK' : K' ‚ü∂ K'') (fL' : L' ‚ü∂ L'') (f_comm' : fK'.f 0 ‚â´ h''.d‚ÇÄ = h'.d‚ÇÄ ‚â´ fL'.f 0)

/-- Connecting complexes is functorial. -/
@[simps]
protected def map : h.cochainComplex ‚ü∂ h'.cochainComplex where
  f
  | .ofNat n => fL.f n
  | .negSucc n => fK.f n
  comm'
  | .ofNat i, _, .refl _ => fL.comm _ _
  | .negSucc 0, _, .refl _ => by simpa
  | .negSucc (i + 1), _, .refl _ => fK.comm _ _

@[simp] lemma map_id : h.map h (ùüô K) (ùüô L) (by simp) = ùüô _ := by ext (m | _ | m) <;> simp; rfl

lemma map_comp_map :
    h.map h' fK fL f_comm ‚â´ h'.map h'' fK' fL' f_comm'
     = h.map h'' (fK ‚â´ fK') (fL ‚â´ fL') (by simp [f_comm', reassoc_of% f_comm]) := by
  ext (m | _ | m) <;> simp; rfl

lemma homologyMap_map_of_eq_succ (n : ‚Ñï) [NeZero n] (m : ‚Ñ§) (hmn : m = n)
    [HasHomology h.cochainComplex m] [HasHomology L n]
    [HasHomology h'.cochainComplex m] [HasHomology L' n] :
    homologyMap (h.map h' fK fL f_comm) m =
    (h.homologyIsoPos n m hmn).hom ‚â´ homologyMap fL n ‚â´ (h'.homologyIsoPos n m hmn).inv := by
  rw [‚Üê cancel_mono (HomologicalComplex.homologyŒπ ..)]
  dsimp [homologyIsoPos]
  simp only [homologyŒπ_naturality, Category.assoc, restrictionHomologyIso_hom_homologyŒπ,
    homologyŒπ_naturality_assoc, restrictionHomologyIso_inv_homologyŒπ_assoc]
  congr 1
  rw [‚Üê cancel_epi (HomologicalComplex.pOpcycles ..)]
  subst hmn
  simp

lemma homologyMap_map_of_eq_neg_succ (n : ‚Ñï) [NeZero n] (m : ‚Ñ§) (hmn : m = -‚Üë(n + 1))
    [HasHomology h.cochainComplex m] [HasHomology K n]
    [HasHomology h'.cochainComplex m] [HasHomology K' n] :
    homologyMap (h.map h' fK fL f_comm) m =
      (h.homologyIsoNeg n m hmn).hom ‚â´ homologyMap fK n ‚â´ (h'.homologyIsoNeg n m hmn).inv := by
  rw [‚Üê cancel_mono (HomologicalComplex.homologyŒπ ..)]
  dsimp [homologyIsoNeg]
  simp only [homologyŒπ_naturality, Category.assoc, restrictionHomologyIso_hom_homologyŒπ,
    homologyŒπ_naturality_assoc, restrictionHomologyIso_inv_homologyŒπ_assoc]
  congr 1
  rw [‚Üê cancel_epi (HomologicalComplex.pOpcycles ..)]
  obtain rfl : m = .negSucc n := hmn
  simp

end ConnectData

end CochainComplex
