/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.HomologicalComplex

/-! # Embeddings of complex shapes

Given two complexes shapes `c : ComplexShape Î¹` and `c' : ComplexShape Î¹'`,
an embedding from `c` to `c'` (`e : c.Embedding c'`) consists of the data
of an injective map `f : Î¹ â†’ Î¹'` such that for all `iâ‚ iâ‚‚ : Î¹`,
`c.Rel iâ‚ iâ‚‚` implies `c'.Rel (e.f iâ‚) (e.f iâ‚‚)`.
We define a type class `e.IsRelIff` to express that this implication is an equivalence.
Other type classes `e.IsTruncLE` and `e.IsTruncGE` are introduced in order to
formalize truncation functors.

This notion first appeared in the Liquid Tensor Experiment, and was developed there
mostly by Johan Commelin, Adam Topaz and JoÃ«l Riou. It shall be used in order to
relate the categories `CochainComplex C â„•` and `ChainComplex C â„•` to `CochainComplex C â„¤`.
It shall also be used in the construction of the canonical t-structure on the derived
category of an abelian category (TODO).

## TODO

Define the following:
- the extension functor `e.extendFunctor C : HomologicalComplex C c â¥¤ HomologicalComplex C c'`
(extending by the zero object outside of the image of `e.f`);
- assuming `e.IsRelIff`, the restriction functor
`e.restrictionFunctor C : HomologicalComplex C c' â¥¤ HomologicalComplex C c`;
- the stupid truncation functor
`e.stupidTruncFunctor C : HomologicalComplex C c' â¥¤ HomologicalComplex C c'` which is
the composition of the two previous functors.
- assuming `e.IsTruncGE`, truncation functors
`e.truncGE'Functor C : HomologicalComplex C c' â¥¤ HomologicalComplex C c` and
`e.truncGEFunctor C : HomologicalComplex C c' â¥¤ HomologicalComplex C c'`, and a natural
transformation `e.Ï€TruncGENatTrans : ðŸ­ _ âŸ¶ e.truncGEFunctor C` which is a quasi-isomorphism
in degrees in the image of `e.f`;
- assuming `e.IsTruncLE`, truncation functors
`e.truncLE'Functor C : HomologicalComplex C c' â¥¤ HomologicalComplex C c` and
`e.truncLEFunctor C : HomologicalComplex C c' â¥¤ HomologicalComplex C c'`, and a natural
transformation `e.Î¹TruncLENatTrans : e.truncGEFunctor C âŸ¶ ðŸ­ _` which is a quasi-isomorphism
in degrees in the image of `e.f`;

-/

namespace ComplexShape

variable {Î¹ Î¹' : Type*} (c : ComplexShape Î¹) (c' : ComplexShape Î¹')

/-- An embedding of a complex shape `c : ComplexShape Î¹` into a complex shape
`c' : ComplexShape Î¹'` consists of a injective map `f : Î¹ â†’ Î¹'` which satisfies
to a compatiblity with respect to the relations `c.Rel` and `c'.Rel`. -/
structure Embedding where
  /-- the map between the underlying types of indices -/
  f : Î¹ â†’ Î¹'
  injective_f : Function.Injective f
  rel {iâ‚ iâ‚‚ : Î¹} (h : c.Rel iâ‚ iâ‚‚) : c'.Rel (f iâ‚) (f iâ‚‚)

namespace Embedding

variable {c c'}
variable (e : Embedding c c')

/-- An embedding of complex shapes `e` satisfies `e.IsRelIff` if the implication
`e.rel` is an equivalence. -/
class IsRelIff : Prop where
  rel' (iâ‚ iâ‚‚ : Î¹) (h : c'.Rel (e.f iâ‚) (e.f iâ‚‚)) : c.Rel iâ‚ iâ‚‚

lemma rel_iff [e.IsRelIff] (iâ‚ iâ‚‚ : Î¹) : c'.Rel (e.f iâ‚) (e.f iâ‚‚) â†” c.Rel iâ‚ iâ‚‚ := by
  constructor
  Â· apply IsRelIff.rel'
  Â· exact e.rel

section

variable (c c')
variable (f : Î¹ â†’ Î¹') (hf : Function.Injective f)
    (iff : âˆ€ (iâ‚ iâ‚‚ : Î¹), c.Rel iâ‚ iâ‚‚ â†” c'.Rel (f iâ‚) (f iâ‚‚))

/-- Constructor for embeddings between complex shapes when we have an equivalence
`âˆ€ (iâ‚ iâ‚‚ : Î¹), c.Rel iâ‚ iâ‚‚ â†” c'.Rel (f iâ‚) (f iâ‚‚)`. -/
@[simps]
def mk' : Embedding c c' where
  f := f
  injective_f := hf
  rel h := (iff _ _).1 h

instance : (mk' c c' f hf iff).IsRelIff where
  rel' _ _ h := (iff _ _).2 h

end

/-- The condition that the image of the map `e.f` of an embedding of
complex shapes `e : Embedding c c'` is stable by `c'.next`. -/
class IsTruncGE extends e.IsRelIff : Prop where
  mem_next {j : Î¹} {k' : Î¹'} (h : c'.Rel (e.f j) k') :
    âˆƒ k, e.f k = k'

/-- The condition that the image of the map `e.f` of an embedding of
complex shapes `e : Embedding c c'` is stable by `c'.prev`. -/
class IsTruncLE extends e.IsRelIff : Prop where
  mem_prev {i' : Î¹'} {j : Î¹} (h : c'.Rel i' (e.f j)) :
    âˆƒ i, e.f i = i'

lemma mem_next [e.IsTruncGE] {j : Î¹} {k' : Î¹'} (h : c'.Rel (e.f j) k') : âˆƒ k, e.f k = k' :=
  IsTruncGE.mem_next h

lemma mem_prev [e.IsTruncLE] {i' : Î¹'} {j : Î¹} (h : c'.Rel i' (e.f j)) : âˆƒ i, e.f i = i' :=
  IsTruncLE.mem_prev h

open Classical in
/-- The map `Î¹' â†’ Option Î¹` which sends `e.f i` to `some i` and the other elements to `none`. -/
noncomputable def r (i' : Î¹') : Option Î¹ :=
  if h : âˆƒ (i : Î¹), e.f i = i'
  then some h.choose
  else none

lemma r_eq_some {i : Î¹} {i' : Î¹'} (hi : e.f i = i') :
    e.r i' = some i := by
  have h : âˆƒ (i : Î¹), e.f i = i' := âŸ¨i, hiâŸ©
  have : h.choose = i := e.injective_f (h.choose_spec.trans (hi.symm))
  dsimp [r]
  rw [dif_pos âŸ¨i, hiâŸ©, this]

lemma r_eq_none (i' : Î¹') (hi : âˆ€ i, e.f i â‰  i') :
    e.r i' = none :=
  dif_neg (by
    rintro âŸ¨i, hi'âŸ©
    exact hi i hi')

@[simp]
lemma r_f (i : Î¹) : e.r (e.f i) = some i := r_eq_some _ rfl

lemma f_eq_of_r_eq_some {i : Î¹} {i' : Î¹'} (hi : e.r i' = some i) :
    e.f i = i' := by
  by_cases h : âˆƒ (k : Î¹), e.f k = i'
  Â· obtain âŸ¨k, rflâŸ© := h
    rw [r_f] at hi
    congr 1
    simpa using hi.symm
  Â· simp [e.r_eq_none i' (by simpa using h)] at hi

/-- The lower boundary of an embedding `e : Embedding c c'`, as a predicate on `Î¹`.
It is satisfied by `j : Î¹` when there exists `i' : Î¹'` not in the image of `e.f`
such that `c'.Rel i' (e.f j)`. -/
def BoundaryGE (j : Î¹) : Prop :=
  c'.Rel (c'.prev (e.f j)) (e.f j) âˆ§ âˆ€ i, Â¬c'.Rel (e.f i) (e.f j)

lemma mem_boundaryGE {i' : Î¹'} {j : Î¹} (hj : c'.Rel i' (e.f j)) (hi' : âˆ€ i, e.f i â‰  i') :
    e.BoundaryGE j := by
  constructor
  Â· simpa only [c'.prev_eq' hj] using hj
  Â· intro i hi
    apply hi' i
    rw [â† c'.prev_eq' hj, c'.prev_eq' hi]

lemma not_mem_next_boundaryGE [e.IsRelIff] {j k : Î¹} (hk : c.Rel j k) :
    Â¬ e.BoundaryGE k := by
  dsimp [BoundaryGE]
  simp only [not_and, not_forall, not_not]
  intro
  exact âŸ¨j, by simpa only [e.rel_iff] using hkâŸ©

variable {e} in
lemma BoundaryGE.not_mem {j : Î¹} (hj : e.BoundaryGE j) {i' : Î¹'} (hi' : c'.Rel i' (e.f j))
    (a : Î¹) : e.f a â‰  i' := fun ha =>
  hj.2 a (by simpa only [ha] using hi')

/-- The upper boundary of an embedding `e : Embedding c c'`, as a predicate on `Î¹`.
It is satisfied by `j : Î¹` when there exists `k' : Î¹'` not in the image of `e.f`
such that `c'.Rel (e.f j) k'`. -/
def BoundaryLE (j : Î¹) : Prop :=
  c'.Rel (e.f j) (c'.next (e.f j)) âˆ§ âˆ€ k, Â¬c'.Rel (e.f j) (e.f k)

lemma mem_boundaryLE {j : Î¹} {k' : Î¹'} (hj : c'.Rel (e.f j) k') (hk' : âˆ€ k, e.f k â‰  k') :
    e.BoundaryLE j := by
  constructor
  Â· simpa only [c'.next_eq' hj] using hj
  Â· intro k hk
    apply hk' k
    rw [â† c'.next_eq' hj, c'.next_eq' hk]

lemma not_mem_prev_boundaryLE [e.IsRelIff] {i j : Î¹} (hi : c.Rel i j) :
    Â¬ e.BoundaryLE i := by
  dsimp [BoundaryLE]
  simp only [not_and, not_forall, not_not]
  intro
  exact âŸ¨j, by simpa only [e.rel_iff] using hiâŸ©

variable {e} in
lemma BoundaryLE.not_mem {j : Î¹} (hj : e.BoundaryLE j) {k' : Î¹'} (hk' : c'.Rel (e.f j) k')
    (a : Î¹) : e.f a â‰  k' := fun ha =>
  hj.2 a (by simpa only [ha] using hk')

end Embedding

/-- The obvious embedding from `up â„•` to `up â„¤`. -/
@[simps!]
def embeddingUpNat : Embedding (up â„•) (up â„¤) :=
  Embedding.mk' _ _ (fun n => n)
    (fun _ _ h => by simpa using h)
    (by dsimp; omega)

/-- The embedding from `down â„•` to `up â„¤` with sends `n` to `-n`. -/
@[simps!]
def embeddingDownNat : Embedding (down â„•) (up â„¤) :=
  Embedding.mk' _ _ (fun n => -n)
    (fun _ _ h => by simpa using h)
    (by dsimp; omega)

instance : embeddingUpNat.IsRelIff := by dsimp [embeddingUpNat]; infer_instance

instance : embeddingDownNat.IsRelIff := by dsimp [embeddingDownNat]; infer_instance

instance : embeddingUpNat.IsTruncGE where
  mem_next {j _} h := âŸ¨j + 1, hâŸ©

instance : embeddingDownNat.IsTruncLE where
  mem_prev {i j} h := âŸ¨j + 1, by dsimp at h âŠ¢; omegaâŸ©

lemma boundaryGE_embeddingUpNat_iff (n : â„•) :
    embeddingUpNat.BoundaryGE n â†” n = 0 := by
  constructor
  Â· intro h
    obtain _|n := n
    Â· rfl
    Â· simpa using h.2 n
  Â· rintro rfl
    constructor
    Â· simp
    Â· intro i hi
      dsimp at hi
      omega

lemma boundaryLE_embeddingDownNat_iff (n : â„•) :
    embeddingDownNat.BoundaryLE n â†” n = 0 := by
  constructor
  Â· intro h
    obtain _|n := n
    Â· rfl
    Â· simpa using h.2 n
  Â· rintro rfl
    constructor
    Â· simp
    Â· intro i hi
      dsimp at hi
      omega

end ComplexShape

lemma Option.eq_none_or_eq_some {Î¹ : Type*} (x : Option Î¹) :
    x = none âˆ¨ âˆƒ y, x = some y := by
  cases x <;> aesop
