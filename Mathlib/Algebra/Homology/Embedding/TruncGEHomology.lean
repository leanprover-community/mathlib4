/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.Embedding.ExtendHomology
import Mathlib.Algebra.Homology.Embedding.TruncGE
import Mathlib.Algebra.Homology.Embedding.RestrictionHomology
import Mathlib.Algebra.Homology.QuasiIso

/-! # The homology of a canonical truncation

Given an embedding of complex shapes `e : Embedding c c'`,
we relate the homology of `K : HomologicalComplex C c'` and of
`K.truncGE e : HomologicalComplex C c'`.

The main result is that `K.Ï€TruncGE e : K âŸ¶ K.truncGE e` induces a
quasi-isomorphism in degree `e.f i` for all `i`. (Note that the complex
`K.truncGE e` is exact in degrees that are not in the image of `e.f`.)

-/

open CategoryTheory Category Limits

namespace HomologicalComplex

variable {Î¹ Î¹' : Type*} {c : ComplexShape Î¹} {c' : ComplexShape Î¹'}
  {C : Type*} [Category C] [HasZeroMorphisms C]
  (K L : HomologicalComplex C c') (Ï† : K âŸ¶ L) (e : c.Embedding c') [e.IsTruncGE]
  [âˆ€ i', K.HasHomology i'] [âˆ€ i', L.HasHomology i']

namespace truncGE'

variable (i j k : Î¹) (hi : c.prev j = i) (hk : c.next j = k)

include hi hk in
lemma hasHomology_sc'_of_not_mem_boundary (hj : Â¬ e.BoundaryGE j) :
    ((K.truncGE' e).sc' i j k).HasHomology := by
  have : (K.restriction e).HasHomology j :=
    restriction.hasHomology K e i j k hi hk rfl rfl rfl
      (e.prev_f_of_not_boundaryGE hi hj) (e.next_f hk)
  have := ShortComplex.hasHomology_of_iso ((K.restriction e).isoSc' i j k hi hk)
  let Ï† := (shortComplexFunctor' C c i j k).map (K.restrictionToTruncGE' e)
  have : Epi Ï†.Ï„â‚ := by dsimp [Ï†]; infer_instance
  have : IsIso Ï†.Ï„â‚‚ := K.isIso_restrictionToTruncGE' e j hj
  have : IsIso Ï†.Ï„â‚ƒ := K.isIso_restrictionToTruncGE' e k (e.not_boundaryGE_next' hj hk)
  exact ShortComplex.hasHomology_of_epi_of_isIso_of_mono Ï†

lemma hasHomology_of_not_mem_boundary (hj : Â¬ e.BoundaryGE j) :
    (K.truncGE' e).HasHomology j :=
  hasHomology_sc'_of_not_mem_boundary K e _ j _ rfl rfl hj

/-- `K.restrictionToTruncGE' e` is a quasi-isomorphism in degrees that are not at the boundary. -/
lemma quasiIsoAt_restrictionToTruncGE' (hj : Â¬ e.BoundaryGE j)
    [(K.restriction e).HasHomology j] [(K.truncGE' e).HasHomology j] :
    QuasiIsoAt (K.restrictionToTruncGE' e) j := by
  rw [quasiIsoAt_iff]
  let Ï† := (shortComplexFunctor C c j).map (K.restrictionToTruncGE' e)
  have : Epi Ï†.Ï„â‚ := by dsimp [Ï†]; infer_instance
  have : IsIso Ï†.Ï„â‚‚ := K.isIso_restrictionToTruncGE' e j hj
  have : IsIso Ï†.Ï„â‚ƒ := K.isIso_restrictionToTruncGE' e _ (e.not_boundaryGE_next' hj rfl)
  exact ShortComplex.quasiIso_of_epi_of_isIso_of_mono Ï†

section

variable {j' : Î¹'} (hj' : e.f j = j') (hj : e.BoundaryGE j)

lemma homologyÎ¹_truncGE'XIsoOpcycles_inv_d :
    (K.homologyÎ¹ j' â‰« (K.truncGE'XIsoOpcycles e hj' hj).inv) â‰« (K.truncGE' e).d j k = 0 := by
  by_cases hjk : c.Rel j k
  Â· rw [K.truncGE'_d_eq_fromOpcycles e hjk hj' rfl hj, assoc, Iso.inv_hom_id_assoc,
    homologyÎ¹_comp_fromOpcycles_assoc, zero_comp]
  Â· rw [shape _ _ _ hjk, comp_zero]

/-- Auxiliary definition for `truncGE'.homologyData`. -/
noncomputable def isLimitKernelFork :
    IsLimit (KernelFork.ofÎ¹ _ (homologyÎ¹_truncGE'XIsoOpcycles_inv_d K e j k hj' hj)) := by
  have hk' : c'.next j' = e.f k := by simpa only [hj'] using e.next_f hk
  by_cases hjk : c.Rel j k
  Â· let e : parallelPair ((K.truncGE' e).d j k) 0 â‰…
        parallelPair (K.fromOpcycles j' (e.f k)) 0 :=
      parallelPair.ext (K.truncGE'XIsoOpcycles e hj' hj)
        (K.truncGE'XIso e rfl (e.not_boundaryGE_next hjk))
        (by simp [K.truncGE'_d_eq_fromOpcycles e hjk hj' rfl hj]) (by simp)
    exact (IsLimit.postcomposeHomEquiv e _).1
      (IsLimit.ofIsoLimit (K.homologyIsKernel _ _ hk')
      (Fork.ext (Iso.refl _) (by simp [e, Fork.Î¹])))
  Â· have := K.isIso_homologyÎ¹ _ _ hk'
      (shape _ _ _ (by simpa only [â† hj', e.rel_iff] using hjk))
    exact IsLimit.ofIsoLimit (KernelFork.IsLimit.ofId _ (shape _ _ _ hjk))
      (Fork.ext ((truncGE'XIsoOpcycles K e hj' hj) â‰ªâ‰« (asIso (K.homologyÎ¹ j')).symm))

/-- When `j` is at the boundary of the embedding `e` of complex shapes,
this is a homology data for `K.truncGE' e` in degree `j`: the homology is
given by `K.homology j'` where `e.f j = j'`. -/
noncomputable def homologyData :
    ((K.truncGE' e).sc' i j k).HomologyData :=
  ShortComplex.HomologyData.ofIsLimitKernelFork _
    ((K.truncGE' e).shape _ _ (fun hij => e.not_boundaryGE_next hij hj)) _
    (isLimitKernelFork K e j k hk hj' hj)

/-- Computation of the `right.g'` field of `truncGE'.homologyData K e i j k hk hj' hj`. -/
@[simp]
lemma homologyData_right_g' :
    (homologyData K e i j k hk hj' hj).right.g' = (K.truncGE' e).d j k := rfl

end

instance truncGE'_hasHomology (i : Î¹) : (K.truncGE' e).HasHomology i := by
  by_cases hi : e.BoundaryGE i
  Â· exact ShortComplex.HasHomology.mk' (homologyData K e _ _ _ rfl rfl hi)
  Â· exact hasHomology_of_not_mem_boundary K e i hi

end truncGE'

variable [HasZeroObject C]

namespace truncGE

instance (i' : Î¹') : (K.truncGE e).HasHomology i' := by
  dsimp [truncGE]
  infer_instance

/-- The right homology data which allows to show that `K.Ï€TruncGE e`
induces an isomorphism in homology in degrees `j'` such that `e.f j = j'` for some `j`. -/
@[simps]
noncomputable def rightHomologyMapData {i j k : Î¹} {j' : Î¹'} (hj' : e.f j = j')
    (hi : c.prev j = i) (hk : c.next j = k) (hj : e.BoundaryGE j) :
    ShortComplex.RightHomologyMapData ((shortComplexFunctor C c' j').map (K.Ï€TruncGE e))
    (ShortComplex.RightHomologyData.canonical (K.sc j'))
    (extend.rightHomologyData (K.truncGE' e) e hj' hi rfl hk rfl
      (truncGE'.homologyData K e i j k hk hj' hj).right) where
  Ï†Q := (K.truncGE'XIsoOpcycles e hj' hj).inv
  Ï†H := ğŸ™ _
  commp := by
    change K.pOpcycles j' â‰« _ = _
    simp [truncGE'.homologyData, Ï€TruncGE, e.liftExtend_f _ _ hj',
      K.restrictionToTruncGE'_f_eq_iso_hom_pOpcycles_iso_inv e hj' hj]
  commg' := by
    have hk' : e.f k = c'.next j' := by rw [â† hj', e.next_f hk]
    dsimp
    rw [extend.rightHomologyData_g' _ _ _ _ _ _ _ _ hk', Ï€TruncGE,
        e.liftExtend_f _ _ hk', truncGE'.homologyData_right_g']
    by_cases hjk : c.Rel j k
    Â· simp [K.truncGE'_d_eq_fromOpcycles e hjk hj' hk' hj,
        K.restrictionToTruncGE'_f_eq_iso_hom_iso_inv e hk' (e.not_boundaryGE_next hjk)]
      rfl
    Â· obtain rfl : k = j := by rw [â† c.next_eq_self j  (by simpa only [hk] using hjk), hk]
      rw [shape _ _ _ hjk, zero_comp, comp_zero,
        K.restrictionToTruncGE'_f_eq_iso_hom_pOpcycles_iso_inv e hk' hj]
      simp only [restriction_X, restrictionXIso, eqToIso.inv, eqToIso.hom, assoc,
        eqToHom_trans_assoc, eqToHom_refl, id_comp]
      change 0 = K.fromOpcycles _ _ â‰« _
      rw [â† cancel_epi (K.pOpcycles _), comp_zero, p_fromOpcycles_assoc,
        d_pOpcycles_assoc, zero_comp]

end truncGE

lemma quasiIsoAt_Ï€TruncGE {j : Î¹} {j' : Î¹'} (hj' : e.f j = j') :
    QuasiIsoAt (K.Ï€TruncGE e) j' := by
  rw [quasiIsoAt_iff]
  by_cases hj : e.BoundaryGE j
  Â· rw [(truncGE.rightHomologyMapData K e hj' rfl rfl hj).quasiIso_iff]
    dsimp
    infer_instance
  Â· let Ï† := (shortComplexFunctor C c' j').map (K.Ï€TruncGE e)
    have : Epi Ï†.Ï„â‚ := by
      by_cases hi : âˆƒ i, e.f i = c'.prev j'
      Â· obtain âŸ¨i, hiâŸ© := hi
        dsimp [Ï†, Ï€TruncGE]
        rw [e.epi_liftExtend_f_iff _ _ hi]
        infer_instance
      Â· apply IsZero.epi (isZero_extend_X _ _ _ (by simpa using hi))
    have : IsIso Ï†.Ï„â‚‚ := by
      dsimp [Ï†, Ï€TruncGE]
      rw [e.isIso_liftExtend_f_iff _ _ hj']
      exact K.isIso_restrictionToTruncGE' e j hj
    have : IsIso Ï†.Ï„â‚ƒ := by
      dsimp [Ï†, Ï€TruncGE]
      have : c'.next j' = e.f (c.next j) := by simpa only [â† hj'] using e.next_f rfl
      rw [e.isIso_liftExtend_f_iff _ _ this.symm]
      exact K.isIso_restrictionToTruncGE' e _ (e.not_boundaryGE_next' hj rfl)
    exact ShortComplex.quasiIso_of_epi_of_isIso_of_mono Ï†

instance (i : Î¹) : QuasiIsoAt (K.Ï€TruncGE e) (e.f i) := K.quasiIsoAt_Ï€TruncGE e rfl

lemma quasiIso_Ï€TruncGE_iff_isSupported :
    QuasiIso (K.Ï€TruncGE e) â†” K.IsSupported e := by
  constructor
  Â· intro
    refine âŸ¨fun i' hi' => ?_âŸ©
    rw [exactAt_iff_of_quasiIsoAt (K.Ï€TruncGE e) i']
    exact (K.truncGE e).exactAt_of_isSupported e i' hi'
  Â· intro
    rw [quasiIso_iff]
    intro i'
    by_cases hi' : âˆƒ i, e.f i = i'
    Â· obtain âŸ¨i, rflâŸ© := hi'
      infer_instance
    Â· rw [quasiIsoAt_iff_exactAt (K.Ï€TruncGE e) i']
      all_goals exact exactAt_of_isSupported _ e i' (by simpa using hi')

lemma acyclic_truncGE_iff_isSupportedOutside :
    (K.truncGE e).Acyclic â†” K.IsSupportedOutside e := by
  constructor
  Â· intro hK
    exact âŸ¨fun i =>
      by simpa only [exactAt_iff_of_quasiIsoAt (K.Ï€TruncGE e)] using hK (e.f i)âŸ©
  Â· intro hK i'
    by_cases hi' : âˆƒ i, e.f i = i'
    Â· obtain âŸ¨i, rflâŸ© := hi'
      simpa only [â† exactAt_iff_of_quasiIsoAt (K.Ï€TruncGE e)] using hK.exactAt i
    Â· exact exactAt_of_isSupported _ e i' (by simpa using hi')

variable {K L}

lemma quasiIso_truncGEMap_iff :
    QuasiIso (truncGEMap Ï† e) â†” âˆ€ (i : Î¹) (i' : Î¹') (_ : e.f i = i'), QuasiIsoAt Ï† i' := by
  have : âˆ€ (i : Î¹) (i' : Î¹') (_ : e.f i = i'),
      QuasiIsoAt (truncGEMap Ï† e) i' â†” QuasiIsoAt Ï† i' := by
    rintro i _ rfl
    rw [â† quasiIsoAt_iff_comp_left (K.Ï€TruncGE e), Ï€TruncGE_naturality Ï† e,
      quasiIsoAt_iff_comp_right]
  rw [quasiIso_iff]
  constructor
  Â· intro h i i' hi
    simpa only [â† this i i' hi] using h i'
  Â· intro h i'
    by_cases hi' : âˆƒ i, e.f i = i'
    Â· obtain âŸ¨i, hiâŸ© := hi'
      simpa only [this i i' hi] using h i i' hi
    Â· rw [quasiIsoAt_iff_exactAt]
      all_goals exact exactAt_of_isSupported _ e i' (by simpa using hi')

end HomologicalComplex
