/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Homology.Embedding.ExtendHomology
import Mathlib.Algebra.Homology.Embedding.TruncGE
import Mathlib.Algebra.Homology.Embedding.RestrictionHomology
import Mathlib.Algebra.Homology.QuasiIso

/-! # The homology of a canonical truncation

Given an embedding of complex shapes `e : Embedding c c'`,
we relate the homology of `K : HomologicalComplex C c'` and of
`K.truncGE e : HomologicalComplex C c'`.

The main result is that `K.œÄTruncGE e : K ‚ü∂ K.truncGE e` induces a
quasi-isomorphism in degree `e.f i` for all `i`. (Note that the complex
`K.truncGE e` is exact in degrees that are not in the image of `e.f`.)

-/

open CategoryTheory Category Limits

namespace HomologicalComplex

variable {Œπ Œπ' : Type*} {c : ComplexShape Œπ} {c' : ComplexShape Œπ'}
  {C : Type*} [Category C] [HasZeroMorphisms C]
  (K L : HomologicalComplex C c') (œÜ : K ‚ü∂ L) (e : c.Embedding c') [e.IsTruncGE]
  [‚àÄ i', K.HasHomology i'] [‚àÄ i', L.HasHomology i']

namespace truncGE'

variable (i j k : Œπ) (hi : c.prev j = i) (hk : c.next j = k)

include hi hk in
lemma hasHomology_sc'_of_not_mem_boundary (hj : ¬¨ e.BoundaryGE j) :
    ((K.truncGE' e).sc' i j k).HasHomology := by
  have : (K.restriction e).HasHomology j :=
    restriction.hasHomology K e i j k hi hk rfl rfl rfl
      (e.prev_f_of_not_boundaryGE hi hj) (e.next_f hk)
  have := ShortComplex.hasHomology_of_iso ((K.restriction e).isoSc' i j k hi hk)
  let œÜ := (shortComplexFunctor' C c i j k).map (K.restrictionToTruncGE' e)
  have : Epi œÜ.œÑ‚ÇÅ := by dsimp [œÜ]; infer_instance
  have : IsIso œÜ.œÑ‚ÇÇ := K.isIso_restrictionToTruncGE' e j hj
  have : IsIso œÜ.œÑ‚ÇÉ := K.isIso_restrictionToTruncGE' e k (e.not_boundaryGE_next' hj hk)
  exact ShortComplex.hasHomology_of_epi_of_isIso_of_mono œÜ

lemma hasHomology_of_not_mem_boundary (hj : ¬¨ e.BoundaryGE j) :
    (K.truncGE' e).HasHomology j :=
  hasHomology_sc'_of_not_mem_boundary K e _ j _ rfl rfl hj

/-- `K.restrictionToTruncGE' e` is a quasi-isomorphism in degrees that are not at the boundary. -/
lemma quasiIsoAt_restrictionToTruncGE' (hj : ¬¨ e.BoundaryGE j)
    [(K.restriction e).HasHomology j] [(K.truncGE' e).HasHomology j] :
    QuasiIsoAt (K.restrictionToTruncGE' e) j := by
  rw [quasiIsoAt_iff]
  let œÜ := (shortComplexFunctor C c j).map (K.restrictionToTruncGE' e)
  have : Epi œÜ.œÑ‚ÇÅ := by dsimp [œÜ]; infer_instance
  have : IsIso œÜ.œÑ‚ÇÇ := K.isIso_restrictionToTruncGE' e j hj
  have : IsIso œÜ.œÑ‚ÇÉ := K.isIso_restrictionToTruncGE' e _ (e.not_boundaryGE_next' hj rfl)
  exact ShortComplex.quasiIso_of_epi_of_isIso_of_mono œÜ

section

variable {j' : Œπ'} (hj' : e.f j = j') (hj : e.BoundaryGE j)

lemma homologyŒπ_truncGE'XIsoOpcycles_inv_d :
    (K.homologyŒπ j' ‚â´ (K.truncGE'XIsoOpcycles e hj' hj).inv) ‚â´ (K.truncGE' e).d j k = 0 := by
  by_cases hjk : c.Rel j k
  ¬∑ rw [K.truncGE'_d_eq_fromOpcycles e hjk hj' rfl hj, assoc, Iso.inv_hom_id_assoc,
    homologyŒπ_comp_fromOpcycles_assoc, zero_comp]
  ¬∑ rw [shape _ _ _ hjk, comp_zero]

/-- Auxiliary definition for `truncGE'.homologyData`. -/
noncomputable def isLimitKernelFork :
    IsLimit (KernelFork.ofŒπ _ (homologyŒπ_truncGE'XIsoOpcycles_inv_d K e j k hj' hj)) := by
  have hk' : c'.next j' = e.f k := by simpa only [hj'] using e.next_f hk
  by_cases hjk : c.Rel j k
  ¬∑ let e : parallelPair ((K.truncGE' e).d j k) 0 ‚âÖ
        parallelPair (K.fromOpcycles j' (e.f k)) 0 :=
      parallelPair.ext (K.truncGE'XIsoOpcycles e hj' hj)
        (K.truncGE'XIso e rfl (e.not_boundaryGE_next hjk))
        (by simp [K.truncGE'_d_eq_fromOpcycles e hjk hj' rfl hj]) (by simp)
    exact (IsLimit.postcomposeHomEquiv e _).1
      (IsLimit.ofIsoLimit (K.homologyIsKernel _ _ hk')
      (Fork.ext (Iso.refl _) (by simp [e, Fork.Œπ])))
  ¬∑ have := K.isIso_homologyŒπ _ _ hk'
      (shape _ _ _ (by simpa only [‚Üê hj', e.rel_iff] using hjk))
    exact IsLimit.ofIsoLimit (KernelFork.IsLimit.ofId _ (shape _ _ _ hjk))
      (Fork.ext ((truncGE'XIsoOpcycles K e hj' hj) ‚â™‚â´ (asIso (K.homologyŒπ j')).symm))

/-- When `j` is at the boundary of the embedding `e` of complex shapes,
this is a homology data for `K.truncGE' e` in degree `j`: the homology is
given by `K.homology j'` where `e.f j = j'`. -/
noncomputable def homologyData :
    ((K.truncGE' e).sc' i j k).HomologyData :=
  ShortComplex.HomologyData.ofIsLimitKernelFork _
    ((K.truncGE' e).shape _ _ (fun hij ‚Ü¶ e.not_boundaryGE_next hij hj)) _
    (isLimitKernelFork K e j k hk hj' hj)

/-- Computation of the `right.g'` field of `truncGE'.homologyData K e i j k hk hj' hj`. -/
@[simp]
lemma homologyData_right_g' :
    (homologyData K e i j k hk hj' hj).right.g' = (K.truncGE' e).d j k := rfl

end

instance truncGE'_hasHomology (i : Œπ) : (K.truncGE' e).HasHomology i := by
  by_cases hi : e.BoundaryGE i
  ¬∑ exact ShortComplex.HasHomology.mk' (homologyData K e _ _ _ rfl rfl hi)
  ¬∑ exact hasHomology_of_not_mem_boundary K e i hi

end truncGE'

variable [HasZeroObject C]

namespace truncGE

instance (i' : Œπ') : (K.truncGE e).HasHomology i' := by
  dsimp [truncGE]
  infer_instance

/-- The right homology data which allows to show that `K.œÄTruncGE e`
induces an isomorphism in homology in degrees `j'` such that `e.f j = j'` for some `j`. -/
@[simps]
noncomputable def rightHomologyMapData {i j k : Œπ} {j' : Œπ'} (hj' : e.f j = j')
    (hi : c.prev j = i) (hk : c.next j = k) (hj : e.BoundaryGE j) :
    ShortComplex.RightHomologyMapData ((shortComplexFunctor C c' j').map (K.œÄTruncGE e))
    (ShortComplex.RightHomologyData.canonical (K.sc j'))
    (extend.rightHomologyData (K.truncGE' e) e hj' hi rfl hk rfl
      (truncGE'.homologyData K e i j k hk hj' hj).right) where
  œÜQ := (K.truncGE'XIsoOpcycles e hj' hj).inv
  œÜH := ùüô _
  commp := by
    change K.pOpcycles j' ‚â´ _ = _
    simp [truncGE'.homologyData, œÄTruncGE, e.liftExtend_f _ _ hj',
      K.restrictionToTruncGE'_f_eq_iso_hom_pOpcycles_iso_inv e hj' hj]
  commg' := by
    have hk' : e.f k = c'.next j' := by rw [‚Üê hj', e.next_f hk]
    dsimp
    rw [extend.rightHomologyData_g' _ _ _ _ _ _ _ _ hk', œÄTruncGE,
        e.liftExtend_f _ _ hk', truncGE'.homologyData_right_g']
    by_cases hjk : c.Rel j k
    ¬∑ simp [K.truncGE'_d_eq_fromOpcycles e hjk hj' hk' hj,
        K.restrictionToTruncGE'_f_eq_iso_hom_iso_inv e hk' (e.not_boundaryGE_next hjk)]
      rfl
    ¬∑ obtain rfl : k = j := by rw [‚Üê c.next_eq_self j  (by simpa only [hk] using hjk), hk]
      rw [shape _ _ _ hjk, zero_comp, comp_zero,
        K.restrictionToTruncGE'_f_eq_iso_hom_pOpcycles_iso_inv e hk' hj]
      simp only [restriction_X, restrictionXIso, eqToIso.inv, eqToIso.hom, assoc,
        eqToHom_trans_assoc, eqToHom_refl, id_comp]
      change 0 = K.fromOpcycles _ _ ‚â´ _
      rw [‚Üê cancel_epi (K.pOpcycles _), comp_zero, p_fromOpcycles_assoc,
        d_pOpcycles_assoc, zero_comp]

end truncGE

lemma quasiIsoAt_œÄTruncGE {j : Œπ} {j' : Œπ'} (hj' : e.f j = j') :
    QuasiIsoAt (K.œÄTruncGE e) j' := by
  rw [quasiIsoAt_iff]
  by_cases hj : e.BoundaryGE j
  ¬∑ rw [(truncGE.rightHomologyMapData K e hj' rfl rfl hj).quasiIso_iff]
    dsimp
    infer_instance
  ¬∑ let œÜ := (shortComplexFunctor C c' j').map (K.œÄTruncGE e)
    have : Epi œÜ.œÑ‚ÇÅ := by
      by_cases hi : ‚àÉ i, e.f i = c'.prev j'
      ¬∑ obtain ‚ü®i, hi‚ü© := hi
        dsimp [œÜ, œÄTruncGE]
        rw [e.epi_liftExtend_f_iff _ _ hi]
        infer_instance
      ¬∑ apply IsZero.epi (isZero_extend_X _ _ _ (by simpa using hi))
    have : IsIso œÜ.œÑ‚ÇÇ := by
      dsimp [œÜ, œÄTruncGE]
      rw [e.isIso_liftExtend_f_iff _ _ hj']
      exact K.isIso_restrictionToTruncGE' e j hj
    have : IsIso œÜ.œÑ‚ÇÉ := by
      dsimp [œÜ, œÄTruncGE]
      have : c'.next j' = e.f (c.next j) := by simpa only [‚Üê hj'] using e.next_f rfl
      rw [e.isIso_liftExtend_f_iff _ _ this.symm]
      exact K.isIso_restrictionToTruncGE' e _ (e.not_boundaryGE_next' hj rfl)
    exact ShortComplex.quasiIso_of_epi_of_isIso_of_mono œÜ

instance (i : Œπ) : QuasiIsoAt (K.œÄTruncGE e) (e.f i) := K.quasiIsoAt_œÄTruncGE e rfl

lemma quasiIso_œÄTruncGE_iff_isSupported :
    QuasiIso (K.œÄTruncGE e) ‚Üî K.IsSupported e := by
  constructor
  ¬∑ intro
    refine ‚ü®fun i' hi' ‚Ü¶ ?_‚ü©
    rw [exactAt_iff_of_quasiIsoAt (K.œÄTruncGE e) i']
    exact (K.truncGE e).exactAt_of_isSupported e i' hi'
  ¬∑ intro
    rw [quasiIso_iff]
    intro i'
    by_cases hi' : ‚àÉ i, e.f i = i'
    ¬∑ obtain ‚ü®i, rfl‚ü© := hi'
      infer_instance
    ¬∑ rw [quasiIsoAt_iff_exactAt (K.œÄTruncGE e) i']
      all_goals exact exactAt_of_isSupported _ e i' (by simpa using hi')

lemma acyclic_truncGE_iff_isSupportedOutside :
    (K.truncGE e).Acyclic ‚Üî K.IsSupportedOutside e := by
  constructor
  ¬∑ intro hK
    exact ‚ü®fun i ‚Ü¶
      by simpa only [exactAt_iff_of_quasiIsoAt (K.œÄTruncGE e)] using hK (e.f i)‚ü©
  ¬∑ intro hK i'
    by_cases hi' : ‚àÉ i, e.f i = i'
    ¬∑ obtain ‚ü®i, rfl‚ü© := hi'
      simpa only [‚Üê exactAt_iff_of_quasiIsoAt (K.œÄTruncGE e)] using hK.exactAt i
    ¬∑ exact exactAt_of_isSupported _ e i' (by simpa using hi')

variable {K L}

lemma quasiIso_truncGEMap_iff :
    QuasiIso (truncGEMap œÜ e) ‚Üî ‚àÄ (i : Œπ) (i' : Œπ') (_ : e.f i = i'), QuasiIsoAt œÜ i' := by
  have : ‚àÄ (i : Œπ) (i' : Œπ') (_ : e.f i = i'),
      QuasiIsoAt (truncGEMap œÜ e) i' ‚Üî QuasiIsoAt œÜ i' := by
    rintro i _ rfl
    rw [‚Üê quasiIsoAt_iff_comp_left (K.œÄTruncGE e), œÄTruncGE_naturality œÜ e,
      quasiIsoAt_iff_comp_right]
  rw [quasiIso_iff]
  constructor
  ¬∑ intro h i i' hi
    simpa only [‚Üê this i i' hi] using h i'
  ¬∑ intro h i'
    by_cases hi' : ‚àÉ i, e.f i = i'
    ¬∑ obtain ‚ü®i, hi‚ü© := hi'
      simpa only [this i i' hi] using h i i' hi
    ¬∑ rw [quasiIsoAt_iff_exactAt]
      all_goals exact exactAt_of_isSupported _ e i' (by simpa using hi')

end HomologicalComplex
