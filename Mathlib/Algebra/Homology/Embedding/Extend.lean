/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.Embedding.IsSupported
import Mathlib.Algebra.Homology.Additive
import Mathlib.Algebra.Homology.Opposite

/-!
# The extension of a homological complex by an embedding of complex shapes

Given an embedding `e : Embedding c c'` of complex shapes,
and `K : HomologicalComplex C c`, we define `K.extend e : HomologicalComplex C c'`, and this
leads to a functor `e.extendFunctor C : HomologicalComplex C c â¥¤ HomologicalComplex C c'`.

This construction first appeared in the Liquid Tensor Experiment.

-/

open CategoryTheory Category Limits ZeroObject

variable {Î¹ Î¹' : Type*} {c : ComplexShape Î¹} {c' : ComplexShape Î¹'}

namespace HomologicalComplex

variable {C : Type*} [Category C] [HasZeroObject C]

section

variable [HasZeroMorphisms C] (K L M : HomologicalComplex C c)
  (Ï† : K âŸ¶ L) (Ï†' : L âŸ¶ M) (e : c.Embedding c')

namespace extend

/-- Auxiliary definition for the `X` field of `HomologicalComplex.extend`. -/
noncomputable def X : Option Î¹ â†’ C
  | some x => K.X x
  | none => 0

/-- The isomorphism `X K i â‰… K.X j` when `i = some j`. -/
noncomputable def XIso {i : Option Î¹} {j : Î¹} (hj : i = some j) :
    X K i â‰… K.X j := eqToIso (by subst hj; rfl)

lemma isZero_X {i : Option Î¹} (hi : i = none) :
    IsZero (X K i) := by
  subst hi
  exact Limits.isZero_zero _

/-- The canonical isomorphism `X K.op i â‰… Opposite.op (X K i)`. -/
noncomputable def XOpIso (i : Option Î¹) : X K.op i â‰… Opposite.op (X K i) :=
  match i with
  | some _ => Iso.refl _
  | none => IsZero.iso (isZero_X _ rfl) (isZero_X K rfl).op

/-- Auxiliary definition for the `d` field of `HomologicalComplex.extend`. -/
noncomputable def d : âˆ€ (i j : Option Î¹), extend.X K i âŸ¶ extend.X K j
  | none, _ => 0
  | some i, some j => K.d i j
  | some _, none => 0

lemma d_none_eq_zero (i j : Option Î¹) (hi : i = none) :
    d K i j = 0 := by subst hi; rfl

lemma d_none_eq_zero' (i j : Option Î¹) (hj : j = none) :
    d K i j = 0 := by subst hj; cases i <;> rfl

lemma d_eq {i j : Option Î¹} {a b : Î¹} (hi : i = some a) (hj : j = some b) :
    d K i j = (XIso K hi).hom â‰« K.d a b â‰« (XIso K hj).inv := by
  subst hi hj
  simp [XIso, X, d]

@[reassoc]
lemma XOpIso_hom_d_op (i j : Option Î¹) :
    (XOpIso K i).hom â‰« (d K j i).op =
      d K.op i j â‰« (XOpIso K j).hom :=
  match i, j with
  | none, _ => by
      simp only [d_none_eq_zero, d_none_eq_zero', comp_zero, zero_comp, op_zero]
  | some i, some j => by
      dsimp [XOpIso]
      simp only [d_eq _ rfl rfl, op_comp, assoc, id_comp, comp_id]
      rfl
  | some _, none => by
      simp only [d_none_eq_zero, d_none_eq_zero', comp_zero, zero_comp, op_zero]

variable {K L}

/-- Auxiliary definition for `HomologicalComplex.extendMap`. -/
noncomputable def mapX : âˆ€ (i : Option Î¹), X K i âŸ¶ X L i
  | some i => Ï†.f i
  | none => 0

lemma mapX_some {i : Option Î¹} {a : Î¹} (hi : i = some a) :
    mapX Ï† i = (XIso K hi).hom â‰« Ï†.f a â‰« (XIso L hi).inv := by
  subst hi
  dsimp [XIso, X]
  rw [id_comp, comp_id]
  rfl

lemma mapX_none {i : Option Î¹} (hi : i = none) :
    mapX Ï† i = 0 := by subst hi; rfl

end extend

/-- Given `K : HomologicalComplex C c` and `e : c.Embedding c'`,
this is the extension of `K` in `HomologicalComplex C c'`: it is
zero in the degrees that are not in the image of `e.f`. -/
noncomputable def extend : HomologicalComplex C c' where
  X i' := extend.X K (e.r i')
  d i' j' := extend.d K (e.r i') (e.r j')
  shape i' j' h := by
    obtain hi'|âŸ¨i, hiâŸ© := (e.r i').eq_none_or_eq_some
    Â· rw [extend.d_none_eq_zero K _ _ hi']
    Â· obtain hj'|âŸ¨j, hjâŸ© := (e.r j').eq_none_or_eq_some
      Â· rw [extend.d_none_eq_zero' K _ _ hj']
      Â· rw [extend.d_eq K hi hj,K.shape, zero_comp, comp_zero]
        obtain rfl := e.f_eq_of_r_eq_some hi
        obtain rfl := e.f_eq_of_r_eq_some hj
        intro hij
        exact h (e.rel hij)
  d_comp_d' i' j' k' _ _ := by
    obtain hi'|âŸ¨i, hiâŸ© := (e.r i').eq_none_or_eq_some
    Â· rw [extend.d_none_eq_zero K _ _ hi', zero_comp]
    Â· obtain hj'|âŸ¨j, hjâŸ© := (e.r j').eq_none_or_eq_some
      Â· rw [extend.d_none_eq_zero K _ _ hj', comp_zero]
      Â· obtain hk'|âŸ¨k, hkâŸ© := (e.r k').eq_none_or_eq_some
        Â· rw [extend.d_none_eq_zero' K _ _ hk', comp_zero]
        Â· rw [extend.d_eq K hi hj, extend.d_eq K hj hk, assoc, assoc,
            Iso.inv_hom_id_assoc, K.d_comp_d_assoc, zero_comp, comp_zero]

/-- The isomorphism `(K.extend e).X i' â‰… K.X i` when `e.f i = i'`. -/
noncomputable def extendXIso {i' : Î¹'} {i : Î¹} (h : e.f i = i') :
    (K.extend e).X i' â‰… K.X i :=
  extend.XIso K (e.r_eq_some h)

lemma isZero_extend_X' (i' : Î¹') (hi' : e.r i' = none) :
    IsZero ((K.extend e).X i') :=
  extend.isZero_X K hi'

lemma isZero_extend_X (i' : Î¹') (hi' : âˆ€ i, e.f i â‰  i') :
    IsZero ((K.extend e).X i') :=
  K.isZero_extend_X' e i' (by
    obtain hi'|âŸ¨i, hiâŸ© := (e.r i').eq_none_or_eq_some
    Â· exact hi'
    Â· exfalso
      exact hi' _ (e.f_eq_of_r_eq_some hi))

instance : (K.extend e).IsStrictlySupported e where
  isZero i' hi' := K.isZero_extend_X e i' hi'

lemma extend_d_eq {i' j' : Î¹'} {i j : Î¹} (hi : e.f i = i') (hj : e.f j = j') :
    (K.extend e).d i' j' = (K.extendXIso e hi).hom â‰« K.d i j â‰«
      (K.extendXIso e hj).inv := by
  apply extend.d_eq

lemma extend_d_from_eq_zero (i' j' : Î¹') (i : Î¹) (hi : e.f i = i') (hi' : Â¬ c.Rel i (c.next i)) :
    (K.extend e).d i' j' = 0 := by
  obtain hj'|âŸ¨j, hjâŸ© := (e.r j').eq_none_or_eq_some
  Â· exact extend.d_none_eq_zero' _ _ _ hj'
  Â· rw [extend_d_eq K e hi (e.f_eq_of_r_eq_some hj), K.shape, zero_comp, comp_zero]
    intro hij
    obtain rfl := c.next_eq' hij
    exact hi' hij

lemma extend_d_to_eq_zero (i' j' : Î¹') (j : Î¹) (hj : e.f j = j') (hj' : Â¬ c.Rel (c.prev j) j) :
    (K.extend e).d i' j' = 0 := by
  obtain hi'|âŸ¨i, hiâŸ© := (e.r i').eq_none_or_eq_some
  Â· exact extend.d_none_eq_zero _ _ _ hi'
  Â· rw [extend_d_eq K e (e.f_eq_of_r_eq_some hi) hj, K.shape, zero_comp, comp_zero]
    intro hij
    obtain rfl := c.prev_eq' hij
    exact hj' hij

variable {K L M}

/-- Given an embedding `e : c.Embedding c'` of complexes shapes, this is the
morphism `K.extend e âŸ¶ L.extend e` induced by a morphism `K âŸ¶ L` in
`HomologicalComplex C c`. -/
noncomputable def extendMap : K.extend e âŸ¶ L.extend e where
  f _ := extend.mapX Ï† _
  comm' i' j' _ := by
    by_cases hi : âˆƒ i, e.f i = i'
    Â· obtain âŸ¨i, hiâŸ© := hi
      by_cases hj : âˆƒ j, e.f j = j'
      Â· obtain âŸ¨j, hjâŸ© := hj
        rw [K.extend_d_eq e hi hj, L.extend_d_eq e hi hj,
          extend.mapX_some Ï† (e.r_eq_some hi),
          extend.mapX_some Ï† (e.r_eq_some hj)]
        simp only [extendXIso, assoc, Iso.inv_hom_id_assoc, Hom.comm_assoc]
      Â· have hj' := e.r_eq_none j' (fun j'' hj'' => hj âŸ¨j'', hj''âŸ©)
        dsimp [extend]
        rw [extend.d_none_eq_zero' _ _ _ hj', extend.d_none_eq_zero' _ _ _ hj',
          comp_zero, zero_comp]
    Â· have hi' := e.r_eq_none i' (fun i'' hi'' => hi âŸ¨i'', hi''âŸ©)
      dsimp [extend]
      rw [extend.d_none_eq_zero _ _ _ hi', extend.d_none_eq_zero _ _ _ hi',
        comp_zero, zero_comp]

lemma extendMap_f {i : Î¹} {i' : Î¹'} (h : e.f i = i') :
    (extendMap Ï† e).f i' =
      (extendXIso K e h).hom â‰« Ï†.f i â‰« (extendXIso L e h).inv := by
  dsimp [extendMap]
  rw [extend.mapX_some Ï† (e.r_eq_some h)]
  rfl

lemma extendMap_f_eq_zero (i' : Î¹') (hi' : âˆ€ i, e.f i â‰  i') :
    (extendMap Ï† e).f i' = 0 := by
  dsimp [extendMap]
  rw [extend.mapX_none Ï† (e.r_eq_none i' hi')]

@[reassoc, simp]
lemma extendMap_comp :
    extendMap (Ï† â‰« Ï†') e = extendMap Ï† e â‰« extendMap Ï†' e := by
  ext i'
  by_cases hi' : âˆƒ i, e.f i = i'
  Â· obtain âŸ¨i, hiâŸ© := hi'
    simp [extendMap_f _ e hi]
  Â· simp [extendMap_f_eq_zero _ e i' (fun i hi => hi' âŸ¨i, hiâŸ©)]

variable (K L M)

lemma extendMap_id_f (i' : Î¹') : (extendMap (ðŸ™ K) e).f i' = ðŸ™ _ := by
  by_cases hi' : âˆƒ i, e.f i = i'
  Â· obtain âŸ¨i, hiâŸ© := hi'
    simp [extendMap_f _ e hi]
  Â· apply (K.isZero_extend_X e i' (fun i hi => hi' âŸ¨i, hiâŸ©)).eq_of_src

@[simp]
lemma extendMap_id : extendMap (ðŸ™ K) e = ðŸ™ _ := by
  ext i'
  by_cases hi' : âˆƒ i, e.f i = i'
  Â· obtain âŸ¨i, hiâŸ© := hi'
    simp [extendMap_f _ e hi]
  Â· apply (K.isZero_extend_X e i' (fun i hi => hi' âŸ¨i, hiâŸ©)).eq_of_src

@[simp]
lemma extendMap_zero : extendMap (0 : K âŸ¶ L) e = 0 := by
  ext i'
  by_cases hi' : âˆƒ i, e.f i = i'
  Â· obtain âŸ¨i, hiâŸ© := hi'
    simp [extendMap_f _ e hi]
  Â· apply (K.isZero_extend_X e i' (fun i hi => hi' âŸ¨i, hiâŸ©)).eq_of_src

/-- The canonical isomorphism `K.op.extend e.op â‰… (K.extend e).op`. -/
noncomputable def extendOpIso : K.op.extend e.op â‰… (K.extend e).op :=
  Hom.isoOfComponents (fun _ â†¦ extend.XOpIso _ _) (fun _ _ _ â†¦
    extend.XOpIso_hom_d_op _ _ _)

@[reassoc]
lemma extend_op_d (i' j' : Î¹') :
    (K.op.extend e.op).d i' j' =
      (K.extendOpIso e).hom.f i' â‰« ((K.extend e).d j' i').op â‰«
        (K.extendOpIso e).inv.f j' := by
  have := (K.extendOpIso e).inv.comm i' j'
  dsimp at this
  rw [â† this, â† comp_f_assoc, Iso.hom_inv_id, id_f, id_comp]

end

@[simp]
lemma extendMap_add [Preadditive C] {K L : HomologicalComplex C c} (Ï† Ï†' : K âŸ¶ L)
    (e : c.Embedding c') : extendMap (Ï† + Ï†' : K âŸ¶ L) e = extendMap Ï† e + extendMap Ï†' e := by
  ext i'
  by_cases hi' : âˆƒ i, e.f i = i'
  Â· obtain âŸ¨i, hiâŸ© := hi'
    simp [extendMap_f _ e hi]
  Â· apply (K.isZero_extend_X e i' (fun i hi => hi' âŸ¨i, hiâŸ©)).eq_of_src

end HomologicalComplex

namespace ComplexShape.Embedding

variable (e : Embedding c c') (C : Type*) [Category C] [HasZeroObject C]

/-- Given an embedding `e : c.Embedding c'` of complex shapes, this is
the functor `HomologicalComplex C c â¥¤ HomologicalComplex C c'` which
extend complexes along `e`: the extended complexes are zero
in the degrees that are not in the image of `e.f`. -/
@[simps]
noncomputable def extendFunctor [HasZeroMorphisms C] :
    HomologicalComplex C c â¥¤ HomologicalComplex C c' where
  obj K := K.extend e
  map Ï† := HomologicalComplex.extendMap Ï† e

instance [HasZeroMorphisms C] : (e.extendFunctor C).PreservesZeroMorphisms where

instance [Preadditive C] : (e.extendFunctor C).Additive where

end ComplexShape.Embedding
