/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.Algebra.Homology.Embedding.ComplementaryTrunc
public import Mathlib.Algebra.Homology.Embedding.TruncLEHomology
public import Mathlib.Algebra.Homology.Embedding.AreComplementary
public import Mathlib.Algebra.Homology.HomotopyCategory.SingleFunctors
public import Mathlib.Algebra.Homology.HomotopyCategory.ShiftSequence

/-!
# Truncations on cochain complexes indexed by the integers.

In this file, we introduce abbreviations for the canonical truncations
`CochainComplex.truncLE`, `CochainComplex.truncGE` of cochain
complexes indexed by `‚Ñ§`, as well as the conditions
`CochainComplex.IsStrictlyLE`, `CochainComplex.IsStrictlyGE`,
`CochainComplex.IsLE`, and `CochainComplex.IsGE`.

-/

@[expose] public section

open CategoryTheory Category Limits ComplexShape ZeroObject

namespace CochainComplex

variable {C : Type*} [Category* C]

open HomologicalComplex

section HasZeroMorphisms

variable [HasZeroMorphisms C] (K L : CochainComplex C ‚Ñ§) (œÜ : K ‚ü∂ L) (e : K ‚âÖ L)

section

variable [HasZeroObject C] [‚àÄ i, K.HasHomology i] [‚àÄ i, L.HasHomology i]

/-- If `K : CochainComplex C ‚Ñ§`, this is the canonical truncation `‚â§ n` of `K`. -/
noncomputable abbrev truncLE (n : ‚Ñ§) : CochainComplex C ‚Ñ§ :=
  HomologicalComplex.truncLE K (embeddingUpIntLE n)

/-- If `K : CochainComplex C ‚Ñ§`, this is the canonical truncation `‚â• n` of `K`. -/
noncomputable abbrev truncGE (n : ‚Ñ§) : CochainComplex C ‚Ñ§ :=
  HomologicalComplex.truncGE K (embeddingUpIntGE n)

/-- The canonical map `K.truncLE n ‚ü∂ K` for `K : CochainComplex C ‚Ñ§`. -/
noncomputable def ŒπTruncLE (n : ‚Ñ§) : K.truncLE n ‚ü∂ K :=
  HomologicalComplex.ŒπTruncLE K (embeddingUpIntLE n)

/-- The canonical map `K ‚ü∂ K.truncGE n` for `K : CochainComplex C ‚Ñ§`. -/
noncomputable def œÄTruncGE (n : ‚Ñ§) : K ‚ü∂ K.truncGE n :=
  HomologicalComplex.œÄTruncGE K (embeddingUpIntGE n)

instance (n : ‚Ñ§) : Mono (K.ŒπTruncLE n) := by
  dsimp only [ŒπTruncLE]
  infer_instance

instance (n : ‚Ñ§) : Epi (K.œÄTruncGE n) := by
  dsimp only [œÄTruncGE]
  infer_instance

lemma isIso_ŒπTruncLE_f (n m : ‚Ñ§) (h : m < n) : IsIso ((K.ŒπTruncLE n).f m) := by
  obtain ‚ü®a, rfl‚ü© : ‚àÉ a, (embeddingUpIntLE n).f a = m := by
    obtain ‚ü®a, ha‚ü© := Int.le.dest h.le
    exact ‚ü®a, by dsimp; omega‚ü©
  apply HomologicalComplex.isIso_ŒπTruncLE_f
  simp only [ComplexShape.boundaryLE_embeddingUpIntLE_iff]
  rintro rfl
  simp at h

lemma isIso_œÄTruncGE_f (n m : ‚Ñ§) (h : n < m) : IsIso ((K.œÄTruncGE n).f m) := by
  obtain ‚ü®a, rfl‚ü© : ‚àÉ a, (embeddingUpIntGE n).f a = m := by
    obtain ‚ü®a, ha‚ü© := Int.le.dest h.le
    exact ‚ü®a, by dsimp; omega‚ü©
  apply HomologicalComplex.isIso_œÄTruncGE_f
  simp only [ComplexShape.boundaryGE_embeddingUpIntGE_iff]
  rintro rfl
  simp at h

section

variable {K L}

/-- The morphism `K.truncLE n ‚ü∂ L.truncLE n` induced by a morphism `K ‚ü∂ L`. -/
noncomputable abbrev truncLEMap (n : ‚Ñ§) : K.truncLE n ‚ü∂ L.truncLE n :=
  HomologicalComplex.truncLEMap œÜ (embeddingUpIntLE n)

/-- The morphism `K.truncGE n ‚ü∂ L.truncGE n` induced by a morphism `K ‚ü∂ L`. -/
noncomputable abbrev truncGEMap (n : ‚Ñ§) : K.truncGE n ‚ü∂ L.truncGE n :=
  HomologicalComplex.truncGEMap œÜ (embeddingUpIntGE n)

@[reassoc (attr := simp)]
lemma ŒπTruncLE_naturality (n : ‚Ñ§) :
    truncLEMap œÜ n ‚â´ L.ŒπTruncLE n = K.ŒπTruncLE n ‚â´ œÜ := by
  apply HomologicalComplex.ŒπTruncLE_naturality

@[reassoc (attr := simp)]
lemma œÄTruncGE_naturality (n : ‚Ñ§) :
    K.œÄTruncGE n ‚â´ truncGEMap œÜ n = œÜ ‚â´ L.œÄTruncGE n := by
  apply HomologicalComplex.œÄTruncGE_naturality

end

end

/-- The condition that a cochain complex `K` is strictly `‚â• n`. -/
abbrev IsStrictlyGE (n : ‚Ñ§) := K.IsStrictlySupported (embeddingUpIntGE n)

/-- The condition that a cochain complex `K` is strictly `‚â§ n`. -/
abbrev IsStrictlyLE (n : ‚Ñ§) := K.IsStrictlySupported (embeddingUpIntLE n)

/-- The condition that a cochain complex `K` is (cohomologically) `‚â• n`. -/
abbrev IsGE (n : ‚Ñ§) := K.IsSupported (embeddingUpIntGE n)

/-- The condition that a cochain complex `K` is (cohomologically) `‚â§ n`. -/
abbrev IsLE (n : ‚Ñ§) := K.IsSupported (embeddingUpIntLE n)

lemma isZero_of_isStrictlyGE (n i : ‚Ñ§) (hi : i < n := by lia) [K.IsStrictlyGE n] :
    IsZero (K.X i) :=
  isZero_X_of_isStrictlySupported K (embeddingUpIntGE n) i
    (by simpa only [notMem_range_embeddingUpIntGE_iff] using hi)

lemma isZero_of_isStrictlyLE (n i : ‚Ñ§) (hi : n < i := by lia) [K.IsStrictlyLE n] :
    IsZero (K.X i) :=
  isZero_X_of_isStrictlySupported K (embeddingUpIntLE n) i
    (by simpa only [notMem_range_embeddingUpIntLE_iff] using hi)

lemma exactAt_of_isGE (n i : ‚Ñ§) (hi : i < n := by lia) [K.IsGE n] :
    K.ExactAt i :=
  exactAt_of_isSupported K (embeddingUpIntGE n) i
    (by simpa only [notMem_range_embeddingUpIntGE_iff] using hi)

lemma exactAt_of_isLE (n i : ‚Ñ§) (hi : n < i := by lia) [K.IsLE n] :
    K.ExactAt i :=
  exactAt_of_isSupported K (embeddingUpIntLE n) i
    (by simpa only [notMem_range_embeddingUpIntLE_iff] using hi)

lemma isZero_of_isGE (n i : ‚Ñ§) (hi : i < n := by lia) [K.IsGE n] [K.HasHomology i] :
    IsZero (K.homology i) :=
  (K.exactAt_of_isGE n i hi).isZero_homology

lemma isZero_of_isLE (n i : ‚Ñ§) (hi : n < i := by lia) [K.IsLE n] [K.HasHomology i] :
    IsZero (K.homology i) :=
  (K.exactAt_of_isLE n i hi).isZero_homology

lemma isStrictlyGE_iff (n : ‚Ñ§) :
    K.IsStrictlyGE n ‚Üî ‚àÄ (i : ‚Ñ§) (_ : i < n := by lia), IsZero (K.X i) := by
  constructor
  ¬∑ intro _ i hi
    exact K.isZero_of_isStrictlyGE n i hi
  ¬∑ intro h
    refine IsStrictlySupported.mk (fun i hi ‚Ü¶ ?_)
    rw [notMem_range_embeddingUpIntGE_iff] at hi
    exact h i hi

lemma isStrictlyLE_iff (n : ‚Ñ§) :
    K.IsStrictlyLE n ‚Üî ‚àÄ (i : ‚Ñ§) (_ : n < i), IsZero (K.X i) := by
  constructor
  ¬∑ intro _ i hi
    exact K.isZero_of_isStrictlyLE n i hi
  ¬∑ intro h
    refine IsStrictlySupported.mk (fun i hi ‚Ü¶ ?_)
    rw [notMem_range_embeddingUpIntLE_iff] at hi
    exact h i hi

lemma isGE_iff (n : ‚Ñ§) :
    K.IsGE n ‚Üî ‚àÄ (i : ‚Ñ§) (_ : i < n), K.ExactAt i := by
  constructor
  ¬∑ intro _ i hi
    exact K.exactAt_of_isGE n i hi
  ¬∑ intro h
    refine IsSupported.mk (fun i hi ‚Ü¶ ?_)
    rw [notMem_range_embeddingUpIntGE_iff] at hi
    exact h i hi

lemma isLE_iff (n : ‚Ñ§) :
    K.IsLE n ‚Üî ‚àÄ (i : ‚Ñ§) (_ : n < i), K.ExactAt i := by
  constructor
  ¬∑ intro _ i hi
    exact K.exactAt_of_isLE n i hi
  ¬∑ intro h
    refine IsSupported.mk (fun i hi ‚Ü¶ ?_)
    rw [notMem_range_embeddingUpIntLE_iff] at hi
    exact h i hi

lemma isStrictlyLE_of_le (p q : ‚Ñ§) (hpq : p ‚â§ q) [K.IsStrictlyLE p] :
    K.IsStrictlyLE q := by
  rw [isStrictlyLE_iff]
  intro i hi
  exact K.isZero_of_isStrictlyLE p _

lemma isStrictlyGE_of_ge (p q : ‚Ñ§) (hpq : p ‚â§ q) [K.IsStrictlyGE q] :
    K.IsStrictlyGE p := by
  rw [isStrictlyGE_iff]
  intro i hi
  exact K.isZero_of_isStrictlyGE q _

lemma isLE_of_le (p q : ‚Ñ§) (hpq : p ‚â§ q) [K.IsLE p] :
    K.IsLE q := by
  rw [isLE_iff]
  intro i hi
  exact K.exactAt_of_isLE p _

lemma isGE_of_ge (p q : ‚Ñ§) (hpq : p ‚â§ q) [K.IsGE q] :
    K.IsGE p := by
  rw [isGE_iff]
  intro i hi
  exact K.exactAt_of_isGE q _

section

variable {K L}

include e

lemma isStrictlyLE_of_iso (n : ‚Ñ§) [K.IsStrictlyLE n] : L.IsStrictlyLE n := by
  apply isStrictlySupported_of_iso e

lemma isStrictlyGE_of_iso (n : ‚Ñ§) [K.IsStrictlyGE n] : L.IsStrictlyGE n := by
  apply isStrictlySupported_of_iso e

lemma isLE_of_iso (n : ‚Ñ§) [K.IsLE n] : L.IsLE n := by
  apply isSupported_of_iso e

lemma isGE_of_iso (n : ‚Ñ§) [K.IsGE n] : L.IsGE n := by
  apply isSupported_of_iso e

end

section

variable [HasZeroObject C]

instance (X : CochainComplex C ‚Ñï) :
    CochainComplex.IsStrictlyGE (X.extend embeddingUpNat) 0 where
  isZero _ _ := isZero_extend_X _ _ _ (by aesop)

instance (X : ChainComplex C ‚Ñï) :
    CochainComplex.IsStrictlyLE (X.extend embeddingDownNat) 0 where
  isZero _ _ := isZero_extend_X _ _ _ (by aesop)

/-- A cochain complex that is both strictly `‚â§ n` and `‚â• n` is isomorphic to
a complex `(single _ _ n).obj M` for some object `M`. -/
lemma exists_iso_single (n : ‚Ñ§) [K.IsStrictlyGE n] [K.IsStrictlyLE n] :
    ‚àÉ (M : C), Nonempty (K ‚âÖ (single _ _ n).obj M) :=
  ‚ü®K.X n, ‚ü®{
      hom := mkHomToSingle (ùüô _) (fun i (hi : i + 1 = n) ‚Ü¶
        (K.isZero_of_isStrictlyGE n i (by lia)).eq_of_src _ _)
      inv := mkHomFromSingle (ùüô _) (fun i (hi : n + 1 = i) ‚Ü¶
        (K.isZero_of_isStrictlyLE n i (by lia)).eq_of_tgt _ _)
      hom_inv_id := by
        ext i
        obtain hi | rfl | hi := lt_trichotomy i n
        ¬∑ apply (K.isZero_of_isStrictlyGE n i (by lia)).eq_of_src
        ¬∑ simp
        ¬∑ apply (K.isZero_of_isStrictlyLE n i (by lia)).eq_of_tgt
      inv_hom_id := by aesop }‚ü©‚ü©

instance (A : C) (n : ‚Ñ§) :
    IsStrictlyGE ((single C (ComplexShape.up ‚Ñ§) n).obj A) n := by
  rw [isStrictlyGE_iff]
  intro i hi
  exact isZero_single_obj_X _ _ _ _ (by lia)

instance (A : C) (n : ‚Ñ§) :
    IsStrictlyLE ((single C (ComplexShape.up ‚Ñ§) n).obj A) n := by
  rw [isStrictlyLE_iff]
  intro i hi
  exact isZero_single_obj_X _ _ _ _ (by lia)

variable [‚àÄ i, K.HasHomology i] [‚àÄ i, L.HasHomology i] (n : ‚Ñ§)

instance [K.IsStrictlyGE n] : IsIso (K.œÄTruncGE n) := by dsimp [œÄTruncGE]; infer_instance

instance [K.IsStrictlyLE n] : IsIso (K.ŒπTruncLE n) := by dsimp [ŒπTruncLE]; infer_instance

lemma isIso_œÄTruncGE_iff : IsIso (K.œÄTruncGE n) ‚Üî K.IsStrictlyGE n := by
  apply HomologicalComplex.isIso_œÄTruncGE_iff

lemma isIso_ŒπTruncLE_iff : IsIso (K.ŒπTruncLE n) ‚Üî K.IsStrictlyLE n := by
  apply HomologicalComplex.isIso_ŒπTruncLE_iff

lemma quasiIso_œÄTruncGE_iff : QuasiIso (K.œÄTruncGE n) ‚Üî K.IsGE n :=
  quasiIso_œÄTruncGE_iff_isSupported K (embeddingUpIntGE n)

lemma quasiIso_ŒπTruncLE_iff : QuasiIso (K.ŒπTruncLE n) ‚Üî K.IsLE n :=
  quasiIso_ŒπTruncLE_iff_isSupported K (embeddingUpIntLE n)

instance [K.IsGE n] : QuasiIso (K.œÄTruncGE n) := by
  rw [quasiIso_œÄTruncGE_iff]
  infer_instance

instance [K.IsLE n] : QuasiIso (K.ŒπTruncLE n) := by
  rw [quasiIso_ŒπTruncLE_iff]
  infer_instance

variable {K L}

lemma quasiIso_truncGEMap_iff :
    QuasiIso (truncGEMap œÜ n) ‚Üî ‚àÄ (i : ‚Ñ§) (_ : n ‚â§ i), QuasiIsoAt œÜ i := by
  rw [HomologicalComplex.quasiIso_truncGEMap_iff]
  constructor
  ¬∑ intro h i hi
    obtain ‚ü®k, rfl‚ü© := Int.le.dest hi
    exact h k _ rfl
  ¬∑ rintro h i i' rfl
    exact h _ (by dsimp; lia)

lemma quasiIso_truncLEMap_iff :
    QuasiIso (truncLEMap œÜ n) ‚Üî ‚àÄ (i : ‚Ñ§) (_ : i ‚â§ n), QuasiIsoAt œÜ i := by
  rw [HomologicalComplex.quasiIso_truncLEMap_iff]
  constructor
  ¬∑ intro h i hi
    obtain ‚ü®k, rfl‚ü© := Int.le.dest hi
    exact h k _ (by dsimp; lia)
  ¬∑ rintro h i i' rfl
    exact h _ (by dsimp; lia)

end

end HasZeroMorphisms

section Preadditive

variable [Preadditive C]

instance [HasZeroObject C] (A : C) (n : ‚Ñ§) : ((singleFunctor C n).obj A).IsStrictlyGE n :=
  inferInstanceAs (IsStrictlyGE ((single C (ComplexShape.up ‚Ñ§) n).obj A) n)

instance [HasZeroObject C] (A : C) (n : ‚Ñ§) : ((singleFunctor C n).obj A).IsStrictlyLE n :=
  inferInstanceAs (IsStrictlyLE ((single C (ComplexShape.up ‚Ñ§) n).obj A) n)

variable (K : CochainComplex C ‚Ñ§)

lemma isStrictlyLE_shift (n : ‚Ñ§) [K.IsStrictlyLE n] (a n' : ‚Ñ§) (h : a + n' = n) :
    (K‚ü¶a‚üß).IsStrictlyLE n' := by
  rw [isStrictlyLE_iff]
  intro i hi
  exact IsZero.of_iso (K.isZero_of_isStrictlyLE n _ (by lia)) (K.shiftFunctorObjXIso a i _ rfl)

lemma isStrictlyGE_shift (n : ‚Ñ§) [K.IsStrictlyGE n] (a n' : ‚Ñ§) (h : a + n' = n) :
    (K‚ü¶a‚üß).IsStrictlyGE n' := by
  rw [isStrictlyGE_iff]
  intro i hi
  exact IsZero.of_iso (K.isZero_of_isStrictlyGE n _ (by lia)) (K.shiftFunctorObjXIso a i _ rfl)

section

variable [CategoryWithHomology C]

lemma isLE_shift (n : ‚Ñ§) [K.IsLE n] (a n' : ‚Ñ§) (h : a + n' = n) : (K‚ü¶a‚üß).IsLE n' := by
  rw [isLE_iff]
  intro i hi
  rw [exactAt_iff_isZero_homology]
  exact IsZero.of_iso (K.isZero_of_isLE n (a + i) (by lia))
    (((homologyFunctor C _ (0 : ‚Ñ§)).shiftIso a i _ rfl).app K)

lemma isGE_shift (n : ‚Ñ§) [K.IsGE n] (a n' : ‚Ñ§) (h : a + n' = n) : (K‚ü¶a‚üß).IsGE n' := by
  rw [isGE_iff]
  intro i hi
  rw [exactAt_iff_isZero_homology]
  exact IsZero.of_iso (K.isZero_of_isGE n (a + i) (by lia))
    (((homologyFunctor C _ (0 : ‚Ñ§)).shiftIso a i _ rfl).app K)

end

end Preadditive

section HasZeroMorphisms

variable {C : Type*} [Category C] [HasZeroMorphisms C] [HasZeroObject C]
  (K L : CochainComplex C ‚Ñ§) (œÜ : K ‚ü∂ L) (e : K ‚âÖ L)

variable [‚àÄ (i : ‚Ñ§), K.HasHomology i] [‚àÄ (i : ‚Ñ§), L.HasHomology i] (n : ‚Ñ§)

instance : QuasiIsoAt (K.œÄTruncGE n) n :=
  K.quasiIsoAt_œÄTruncGE (embeddingUpIntGE n) (j := 0) (by simp)

instance : QuasiIsoAt (K.ŒπTruncLE n) n :=
  K.quasiIsoAt_ŒπTruncLE (embeddingUpIntLE n) (j := 0) (by simp)

noncomputable def truncGEXIso (n : ‚Ñ§) (i : ‚Ñ§) (hi : n < i) :
    (K.truncGE n).X i ‚âÖ K.X i :=
  HomologicalComplex.truncGEXIso K (embeddingUpIntGE n) (i := (i - n).natAbs) (by
      dsimp
      rw [Int.natAbs_of_nonneg (by omega), add_sub_cancel])
    (fun h => by
      rw [boundaryGE_embeddingUpIntGE_iff, Int.natAbs_eq_zero] at h
      lia)

noncomputable def truncGEXIsoOpcycles (n : ‚Ñ§) :
    (K.truncGE n).X n ‚âÖ K.opcycles n :=
  HomologicalComplex.truncGEXIsoOpcycles K (embeddingUpIntGE n) (i := 0) (by simp)
    (by rw [boundaryGE_embeddingUpIntGE_iff])

lemma acyclic_truncGE_iff (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (h : n‚ÇÄ + 1 = n‚ÇÅ) :
    (K.truncGE n‚ÇÅ).Acyclic ‚Üî K.IsLE n‚ÇÄ := by
  dsimp [truncGE]
  rw [acyclic_truncGE_iff_isSupportedOutside,
    (Embedding.embeddingUpInt_areComplementary n‚ÇÄ n‚ÇÅ h).isSupportedOutside‚ÇÇ_iff]

end HasZeroMorphisms

section Abelian

variable [Abelian C] (K L : CochainComplex C ‚Ñ§)

/-- The cokernel sequence of the monomorphism `K.ŒπTruncLE n`. -/
noncomputable abbrev shortComplexTruncLE (n : ‚Ñ§) : ShortComplex (CochainComplex C ‚Ñ§) :=
  HomologicalComplex.shortComplexTruncLE K (embeddingUpIntLE n)

lemma shortComplexTruncLE_shortExact (n : ‚Ñ§) :
    (K.shortComplexTruncLE n).ShortExact := by
  apply HomologicalComplex.shortComplexTruncLE_shortExact

variable (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (h : n‚ÇÄ + 1 = n‚ÇÅ)

/-- The canonical morphism `(K.shortComplexTruncLE n‚ÇÄ).X‚ÇÉ ‚ü∂ K.truncGE n‚ÇÅ`. -/
noncomputable abbrev shortComplexTruncLEX‚ÇÉToTruncGE :
    (K.shortComplexTruncLE n‚ÇÄ).X‚ÇÉ ‚ü∂ K.truncGE n‚ÇÅ :=
  HomologicalComplex.shortComplexTruncLEX‚ÇÉToTruncGE K
    (Embedding.embeddingUpInt_areComplementary n‚ÇÄ n‚ÇÅ h)

@[reassoc (attr := simp)]
lemma g_shortComplexTruncLEX‚ÇÉToTruncGE :
    (K.shortComplexTruncLE n‚ÇÄ).g ‚â´ K.shortComplexTruncLEX‚ÇÉToTruncGE n‚ÇÄ n‚ÇÅ h = K.œÄTruncGE n‚ÇÅ := by
  apply HomologicalComplex.g_shortComplexTruncLEX‚ÇÉToTruncGE

lemma acyclic_truncLE_iff (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (h : n‚ÇÄ + 1 = n‚ÇÅ) :
    (K.truncLE n‚ÇÄ).Acyclic ‚Üî K.IsGE n‚ÇÅ := by
  dsimp [truncLE]
  rw [acyclic_truncLE_iff_isSupportedOutside,
    (Embedding.embeddingUpInt_areComplementary n‚ÇÄ n‚ÇÅ h).isSupportedOutside‚ÇÅ_iff K]

end Abelian

end CochainComplex
