/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.Embedding.AreComplementary
import Mathlib.Algebra.Homology.HomotopyCategory.SingleFunctors
import Mathlib.Algebra.Homology.HomotopyCategory.ShiftSequence

/-!
# Truncations on cochain complexes indexed by the integers.

In this file, we introduce abbreviations for the canonical truncations
`CochainComplex.truncLE`, `CochainComplex.truncGE` of cochain
complexes indexed by `â„¤`, as well as the conditions
`CochainComplex.IsStrictlyLE`, `CochainComplex.IsStrictlyGE`,
`CochainComplex.IsLE`, and `CochainComplex.IsGE`.

-/

open CategoryTheory Category Limits ComplexShape ZeroObject

namespace CochainComplex

variable {C : Type*} [Category C]

open HomologicalComplex

section HasZeroMorphisms

variable [HasZeroMorphisms C] (K L : CochainComplex C â„¤) (Ï† : K âŸ¶ L) (e : K â‰… L)

section

variable [HasZeroObject C] [âˆ€ i, K.HasHomology i] [âˆ€ i, L.HasHomology i]

/-- If `K : CochainComplex C â„¤`, this is the canonical truncation `â‰¤ n` of `K`. -/
noncomputable abbrev truncLE (n : â„¤) : CochainComplex C â„¤ :=
  HomologicalComplex.truncLE K (embeddingUpIntLE n)

/-- If `K : CochainComplex C â„¤`, this is the canonical truncation `â‰¥ n` of `K`. -/
noncomputable abbrev truncGE (n : â„¤) : CochainComplex C â„¤ :=
  HomologicalComplex.truncGE K (embeddingUpIntGE n)

/-- The canonical map `K.truncLE n âŸ¶ K` for `K : CochainComplex C â„¤`. -/
noncomputable def Î¹TruncLE (n : â„¤) : K.truncLE n âŸ¶ K :=
  HomologicalComplex.Î¹TruncLE K (embeddingUpIntLE n)

/-- The canonical map `K âŸ¶ K.truncGE n` for `K : CochainComplex C â„¤`. -/
noncomputable def Ï€TruncGE (n : â„¤) : K âŸ¶ K.truncGE n :=
  HomologicalComplex.Ï€TruncGE K (embeddingUpIntGE n)

section

variable {K L}

/-- The morphism `K.truncLE n âŸ¶ L.truncLE n` induced by a morphism `K âŸ¶ L`. -/
noncomputable abbrev truncLEMap (n : â„¤) : K.truncLE n âŸ¶ L.truncLE n :=
  HomologicalComplex.truncLEMap Ï† (embeddingUpIntLE n)

/-- The morphism `K.truncGE n âŸ¶ L.truncGE n` induced by a morphism `K âŸ¶ L`. -/
noncomputable abbrev truncGEMap (n : â„¤) : K.truncGE n âŸ¶ L.truncGE n :=
  HomologicalComplex.truncGEMap Ï† (embeddingUpIntGE n)

@[reassoc (attr := simp)]
lemma Î¹TruncLE_naturality (n : â„¤) :
    truncLEMap Ï† n â‰« L.Î¹TruncLE n = K.Î¹TruncLE n â‰« Ï† := by
  apply HomologicalComplex.Î¹TruncLE_naturality

@[reassoc (attr := simp)]
lemma Ï€TruncGE_naturality (n : â„¤) :
    K.Ï€TruncGE n â‰« truncGEMap Ï† n = Ï† â‰« L.Ï€TruncGE n := by
  apply HomologicalComplex.Ï€TruncGE_naturality

end

end

/-- The condition that a cochain complex `K` is strictly `â‰¥ n`. -/
abbrev IsStrictlyGE (n : â„¤) := K.IsStrictlySupported (embeddingUpIntGE n)

/-- The condition that a cochain complex `K` is strictly `â‰¤ n`. -/
abbrev IsStrictlyLE (n : â„¤) := K.IsStrictlySupported (embeddingUpIntLE n)

/-- The condition that a cochain complex `K` is (cohomologically) `â‰¥ n`. -/
abbrev IsGE (n : â„¤) := K.IsSupported (embeddingUpIntGE n)

/-- The condition that a cochain complex `K` is (cohomologically) `â‰¤ n`. -/
abbrev IsLE (n : â„¤) := K.IsSupported (embeddingUpIntLE n)

lemma isZero_of_isStrictlyGE (n i : â„¤) (hi : i < n) [K.IsStrictlyGE n] :
    IsZero (K.X i) :=
  isZero_X_of_isStrictlySupported K (embeddingUpIntGE n) i
    (by simpa only [notMem_range_embeddingUpIntGE_iff] using hi)

lemma isZero_of_isStrictlyLE (n i : â„¤) (hi : n < i) [K.IsStrictlyLE n] :
    IsZero (K.X i) :=
  isZero_X_of_isStrictlySupported K (embeddingUpIntLE n) i
    (by simpa only [notMem_range_embeddingUpIntLE_iff] using hi)

lemma exactAt_of_isGE (n i : â„¤) (hi : i < n) [K.IsGE n] :
    K.ExactAt i :=
  exactAt_of_isSupported K (embeddingUpIntGE n) i
    (by simpa only [notMem_range_embeddingUpIntGE_iff] using hi)

lemma exactAt_of_isLE (n i : â„¤) (hi : n < i) [K.IsLE n] :
    K.ExactAt i :=
  exactAt_of_isSupported K (embeddingUpIntLE n) i
    (by simpa only [notMem_range_embeddingUpIntLE_iff] using hi)

lemma isZero_of_isGE (n i : â„¤) (hi : i < n) [K.IsGE n] [K.HasHomology i] :
    IsZero (K.homology i) :=
  (K.exactAt_of_isGE n i hi).isZero_homology

lemma isZero_of_isLE (n i : â„¤) (hi : n < i) [K.IsLE n] [K.HasHomology i] :
    IsZero (K.homology i) :=
  (K.exactAt_of_isLE n i hi).isZero_homology

lemma isStrictlyGE_iff (n : â„¤) :
    K.IsStrictlyGE n â†” âˆ€ (i : â„¤) (_ : i < n), IsZero (K.X i) := by
  constructor
  Â· intro _ i hi
    exact K.isZero_of_isStrictlyGE n i hi
  Â· intro h
    refine IsStrictlySupported.mk (fun i hi â†¦ ?_)
    rw [notMem_range_embeddingUpIntGE_iff] at hi
    exact h i hi

lemma isStrictlyLE_iff (n : â„¤) :
    K.IsStrictlyLE n â†” âˆ€ (i : â„¤) (_ : n < i), IsZero (K.X i) := by
  constructor
  Â· intro _ i hi
    exact K.isZero_of_isStrictlyLE n i hi
  Â· intro h
    refine IsStrictlySupported.mk (fun i hi â†¦ ?_)
    rw [notMem_range_embeddingUpIntLE_iff] at hi
    exact h i hi

lemma isGE_iff (n : â„¤) :
    K.IsGE n â†” âˆ€ (i : â„¤) (_ : i < n), K.ExactAt i := by
  constructor
  Â· intro _ i hi
    exact K.exactAt_of_isGE n i hi
  Â· intro h
    refine IsSupported.mk (fun i hi â†¦ ?_)
    rw [notMem_range_embeddingUpIntGE_iff] at hi
    exact h i hi

lemma isLE_iff (n : â„¤) :
    K.IsLE n â†” âˆ€ (i : â„¤) (_ : n < i), K.ExactAt i := by
  constructor
  Â· intro _ i hi
    exact K.exactAt_of_isLE n i hi
  Â· intro h
    refine IsSupported.mk (fun i hi â†¦ ?_)
    rw [notMem_range_embeddingUpIntLE_iff] at hi
    exact h i hi

lemma isStrictlyLE_of_le (p q : â„¤) (hpq : p â‰¤ q) [K.IsStrictlyLE p] :
    K.IsStrictlyLE q := by
  rw [isStrictlyLE_iff]
  intro i hi
  apply K.isZero_of_isStrictlyLE p
  cutsat

lemma isStrictlyGE_of_ge (p q : â„¤) (hpq : p â‰¤ q) [K.IsStrictlyGE q] :
    K.IsStrictlyGE p := by
  rw [isStrictlyGE_iff]
  intro i hi
  apply K.isZero_of_isStrictlyGE q
  cutsat

lemma isLE_of_le (p q : â„¤) (hpq : p â‰¤ q) [K.IsLE p] :
    K.IsLE q := by
  rw [isLE_iff]
  intro i hi
  apply K.exactAt_of_isLE p
  cutsat

lemma isGE_of_ge (p q : â„¤) (hpq : p â‰¤ q) [K.IsGE q] :
    K.IsGE p := by
  rw [isGE_iff]
  intro i hi
  apply K.exactAt_of_isGE q
  cutsat

section

variable {K L}

include e

lemma isStrictlyLE_of_iso (n : â„¤) [K.IsStrictlyLE n] : L.IsStrictlyLE n := by
  apply isStrictlySupported_of_iso e

lemma isStrictlyGE_of_iso (n : â„¤) [K.IsStrictlyGE n] : L.IsStrictlyGE n := by
  apply isStrictlySupported_of_iso e

lemma isLE_of_iso (n : â„¤) [K.IsLE n] : L.IsLE n := by
  apply isSupported_of_iso e

lemma isGE_of_iso (n : â„¤) [K.IsGE n] : L.IsGE n := by
  apply isSupported_of_iso e

end

section

variable [HasZeroObject C]

/-- A cochain complex that is both strictly `â‰¤ n` and `â‰¥ n` is isomorphic to
a complex `(single _ _ n).obj M` for some object `M`. -/
lemma exists_iso_single (n : â„¤) [K.IsStrictlyGE n] [K.IsStrictlyLE n] :
    âˆƒ (M : C), Nonempty (K â‰… (single _ _ n).obj M) :=
  âŸ¨K.X n, âŸ¨{
      hom := mkHomToSingle (ðŸ™ _) (fun i (hi : i + 1 = n) â†¦
        (K.isZero_of_isStrictlyGE n i (by cutsat)).eq_of_src _ _)
      inv := mkHomFromSingle (ðŸ™ _) (fun i (hi : n + 1 = i) â†¦
        (K.isZero_of_isStrictlyLE n i (by cutsat)).eq_of_tgt _ _)
      hom_inv_id := by
        ext i
        obtain hi | rfl | hi := lt_trichotomy i n
        Â· apply (K.isZero_of_isStrictlyGE n i (by cutsat)).eq_of_src
        Â· simp
        Â· apply (K.isZero_of_isStrictlyLE n i (by cutsat)).eq_of_tgt
      inv_hom_id := by aesop }âŸ©âŸ©

instance (A : C) (n : â„¤) :
    IsStrictlyGE ((single C (ComplexShape.up â„¤) n).obj A) n := by
  rw [isStrictlyGE_iff]
  intro i hi
  exact isZero_single_obj_X _ _ _ _ (by cutsat)

instance (A : C) (n : â„¤) :
    IsStrictlyLE ((single C (ComplexShape.up â„¤) n).obj A) n := by
  rw [isStrictlyLE_iff]
  intro i hi
  exact isZero_single_obj_X _ _ _ _ (by cutsat)

variable [âˆ€ i, K.HasHomology i] [âˆ€ i, L.HasHomology i] (n : â„¤)

instance [K.IsStrictlyGE n] : IsIso (K.Ï€TruncGE n) := by dsimp [Ï€TruncGE]; infer_instance

instance [K.IsStrictlyLE n] : IsIso (K.Î¹TruncLE n) := by dsimp [Î¹TruncLE]; infer_instance

lemma isIso_Ï€TruncGE_iff : IsIso (K.Ï€TruncGE n) â†” K.IsStrictlyGE n := by
  apply HomologicalComplex.isIso_Ï€TruncGE_iff

lemma isIso_Î¹TruncLE_iff : IsIso (K.Î¹TruncLE n) â†” K.IsStrictlyLE n := by
  apply HomologicalComplex.isIso_Î¹TruncLE_iff

lemma quasiIso_Ï€TruncGE_iff : QuasiIso (K.Ï€TruncGE n) â†” K.IsGE n :=
  quasiIso_Ï€TruncGE_iff_isSupported K (embeddingUpIntGE n)

lemma quasiIso_Î¹TruncLE_iff : QuasiIso (K.Î¹TruncLE n) â†” K.IsLE n :=
  quasiIso_Î¹TruncLE_iff_isSupported K (embeddingUpIntLE n)

instance [K.IsGE n] : QuasiIso (K.Ï€TruncGE n) := by
  rw [quasiIso_Ï€TruncGE_iff]
  infer_instance

instance [K.IsLE n] : QuasiIso (K.Î¹TruncLE n) := by
  rw [quasiIso_Î¹TruncLE_iff]
  infer_instance

variable {K L}

lemma quasiIso_truncGEMap_iff :
    QuasiIso (truncGEMap Ï† n) â†” âˆ€ (i : â„¤) (_ : n â‰¤ i), QuasiIsoAt Ï† i := by
  rw [HomologicalComplex.quasiIso_truncGEMap_iff]
  constructor
  Â· intro h i hi
    obtain âŸ¨k, rflâŸ© := Int.le.dest hi
    exact h k _ rfl
  Â· rintro h i i' rfl
    exact h _ (by dsimp; cutsat)

lemma quasiIso_truncLEMap_iff :
    QuasiIso (truncLEMap Ï† n) â†” âˆ€ (i : â„¤) (_ : i â‰¤ n), QuasiIsoAt Ï† i := by
  rw [HomologicalComplex.quasiIso_truncLEMap_iff]
  constructor
  Â· intro h i hi
    obtain âŸ¨k, rflâŸ© := Int.le.dest hi
    exact h k _ (by dsimp; cutsat)
  Â· rintro h i i' rfl
    exact h _ (by dsimp; cutsat)

end

end HasZeroMorphisms

section Preadditive

variable [Preadditive C]

instance [HasZeroObject C] (A : C) (n : â„¤) : ((singleFunctor C n).obj A).IsStrictlyGE n :=
  inferInstanceAs (IsStrictlyGE ((single C (ComplexShape.up â„¤) n).obj A) n)

instance [HasZeroObject C] (A : C) (n : â„¤) : ((singleFunctor C n).obj A).IsStrictlyLE n :=
  inferInstanceAs (IsStrictlyLE ((single C (ComplexShape.up â„¤) n).obj A) n)

variable (K : CochainComplex C â„¤)

lemma isStrictlyLE_shift (n : â„¤) [K.IsStrictlyLE n] (a n' : â„¤) (h : a + n' = n) :
    (KâŸ¦aâŸ§).IsStrictlyLE n' := by
  rw [isStrictlyLE_iff]
  intro i hi
  exact IsZero.of_iso (K.isZero_of_isStrictlyLE n _ (by cutsat)) (K.shiftFunctorObjXIso a i _ rfl)

lemma isStrictlyGE_shift (n : â„¤) [K.IsStrictlyGE n] (a n' : â„¤) (h : a + n' = n) :
    (KâŸ¦aâŸ§).IsStrictlyGE n' := by
  rw [isStrictlyGE_iff]
  intro i hi
  exact IsZero.of_iso (K.isZero_of_isStrictlyGE n _ (by cutsat)) (K.shiftFunctorObjXIso a i _ rfl)

section

variable [CategoryWithHomology C]

lemma isLE_shift (n : â„¤) [K.IsLE n] (a n' : â„¤) (h : a + n' = n) : (KâŸ¦aâŸ§).IsLE n' := by
  rw [isLE_iff]
  intro i hi
  rw [exactAt_iff_isZero_homology]
  exact IsZero.of_iso (K.isZero_of_isLE n (a + i) (by cutsat))
    (((homologyFunctor C _ (0 : â„¤)).shiftIso a i _ rfl).app K)

lemma isGE_shift (n : â„¤) [K.IsGE n] (a n' : â„¤) (h : a + n' = n) : (KâŸ¦aâŸ§).IsGE n' := by
  rw [isGE_iff]
  intro i hi
  rw [exactAt_iff_isZero_homology]
  exact IsZero.of_iso (K.isZero_of_isGE n (a + i) (by cutsat))
    (((homologyFunctor C _ (0 : â„¤)).shiftIso a i _ rfl).app K)

end

end Preadditive


section Abelian

variable [Abelian C] (K L : CochainComplex C â„¤)

/-- The cokernel sequence of the monomorphism `K.Î¹TruncLE n`. -/
noncomputable abbrev shortComplexTruncLE (n : â„¤) : ShortComplex (CochainComplex C â„¤) :=
  HomologicalComplex.shortComplexTruncLE K (embeddingUpIntLE n)

lemma shortComplexTruncLE_shortExact (n : â„¤) :
    (K.shortComplexTruncLE n).ShortExact := by
  apply HomologicalComplex.shortComplexTruncLE_shortExact

variable (nâ‚€ nâ‚ : â„¤) (h : nâ‚€ + 1 = nâ‚)

/-- The canonical morphism `(K.shortComplexTruncLE nâ‚€).Xâ‚ƒ âŸ¶ K.truncGE nâ‚`. -/
noncomputable abbrev shortComplexTruncLEXâ‚ƒToTruncGE :
    (K.shortComplexTruncLE nâ‚€).Xâ‚ƒ âŸ¶ K.truncGE nâ‚ :=
  HomologicalComplex.shortComplexTruncLEXâ‚ƒToTruncGE K
    (Embedding.embeddingUpInt_areComplementary nâ‚€ nâ‚ h)

@[reassoc]
lemma g_shortComplexTruncLEXâ‚ƒToTruncGE :
    (K.shortComplexTruncLE nâ‚€).g â‰« K.shortComplexTruncLEXâ‚ƒToTruncGE nâ‚€ nâ‚ h = K.Ï€TruncGE nâ‚ := by
  apply HomologicalComplex.g_shortComplexTruncLEXâ‚ƒToTruncGE

end Abelian

end CochainComplex
