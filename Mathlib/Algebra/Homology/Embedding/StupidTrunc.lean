/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import Mathlib.Algebra.Homology.Embedding.Extend
import Mathlib.Algebra.Homology.Embedding.IsSupported
import Mathlib.Algebra.Homology.Embedding.Restriction

/-!
# The stupid truncation of homological complexes

Given an embedding `e : c.Embedding c'` of complex shapes, we define
a functor `stupidTruncFunctor : HomologicalComplex C c' â¥¤ HomologicalComplex C c'`
which sends `K` to `K.stupidTrunc e` which is defined as `(K.restriction e).extend e`.

## TODO (@joelriou)
* define the inclusion `e.stupidTruncFunctor C âŸ¶ ğŸ­ _` when `[e.IsTruncGE]`;
* define the projection `ğŸ­ _ âŸ¶ e.stupidTruncFunctor C` when `[e.IsTruncLE]`.

-/

open CategoryTheory Category Limits ZeroObject

variable {Î¹ Î¹' : Type*} {c : ComplexShape Î¹} {c' : ComplexShape Î¹'}

namespace HomologicalComplex

variable {C : Type*} [Category C] [HasZeroMorphisms C] [HasZeroObject C]

variable (K L M : HomologicalComplex C c') (Ï† : K âŸ¶ L) (Ï†' : L âŸ¶ M)
  (e : c.Embedding c') [e.IsRelIff]

/-- The stupid truncation of a complex `K : HomologicalComplex C c'` relatively to
an embedding `e : c.Embedding c'` of complex shapes. -/
noncomputable def stupidTrunc : HomologicalComplex C c' := ((K.restriction e).extend e)

instance : IsStrictlySupported (K.stupidTrunc e) e := by
  dsimp [stupidTrunc]
  infer_instance

/-- The isomorphism `(K.stupidTrunc e).X i' â‰… K.X i'` when `i` is in the image of `e.f`. -/
noncomputable def stupidTruncXIso {i : Î¹} {i' : Î¹'} (hi' : e.f i = i') :
    (K.stupidTrunc e).X i' â‰… K.X i' :=
  (K.restriction e).extendXIso e hi' â‰ªâ‰« eqToIso (by subst hi'; rfl)

lemma isZero_stupidTrunc_X (i' : Î¹') (hi' : âˆ€ i, e.f i â‰  i') :
    IsZero ((K.stupidTrunc e).X i') :=
  isZero_extend_X _ _ _ hi'

instance {Î¹'' : Type*} {c'' : ComplexShape Î¹''} (e' : c''.Embedding c')
    [K.IsStrictlySupported e'] :
    IsStrictlySupported (K.stupidTrunc e) e' where
  isZero i' hi' := by
    by_cases hi'' : âˆƒ i, e.f i = i'
    Â· obtain âŸ¨i, hiâŸ© := hi''
      exact (K.isZero_X_of_isStrictlySupported e' i' hi').of_iso (K.stupidTruncXIso e hi)
    Â· apply isZero_stupidTrunc_X
      simpa using hi''

lemma isZero_stupidTrunc_iff :
    IsZero (K.stupidTrunc e) â†” K.IsStrictlySupportedOutside e := by
  constructor
  Â· exact fun h â†¦ âŸ¨fun i â†¦
      ((eval _ _ (e.f i)).map_isZero h).of_iso (K.stupidTruncXIso e rfl).symmâŸ©
  Â· intro h
    rw [isZero_iff_isStrictlySupported_and_isStrictlySupportedOutside _ e]
    constructor
    Â· infer_instance
    Â· exact âŸ¨fun i â†¦ (h.isZero i).of_iso (K.stupidTruncXIso e rfl)âŸ©

variable {K L M}

/-- The morphism `K.stupidTrunc e âŸ¶ L.stupidTrunc e` induced by a morphism `K âŸ¶ L`. -/
noncomputable def stupidTruncMap : K.stupidTrunc e âŸ¶ L.stupidTrunc e :=
  extendMap (restrictionMap Ï† e) e

variable (K) in
@[simp]
lemma stupidTruncMap_id : stupidTruncMap (ğŸ™ K) e = ğŸ™ _ := by
  simp [stupidTruncMap, stupidTrunc]

@[simp, reassoc]
lemma stupidTruncMap_comp :
    stupidTruncMap (Ï† â‰« Ï†') e = stupidTruncMap Ï† e â‰« stupidTruncMap Ï†' e := by
  simp [stupidTruncMap, stupidTrunc]

@[reassoc (attr := simp)]
lemma stupidTruncMap_stupidTruncXIso_hom {i : Î¹} {i' : Î¹'} (hi : e.f i = i') :
    (stupidTruncMap Ï† e).f i' â‰« (L.stupidTruncXIso e hi).hom =
      (K.stupidTruncXIso e hi).hom â‰« Ï†.f i' := by
  subst hi
  simp [stupidTruncMap, stupidTruncXIso, extendMap_f _ _ rfl]

end HomologicalComplex

namespace ComplexShape.Embedding

variable (e : Embedding c c') (C : Type*) [Category C] [HasZeroMorphisms C] [HasZeroObject C]

/-- The stupid truncation functor `HomologicalComplex C c' â¥¤ HomologicalComplex C c'`
given by an embedding `e : Embedding c c'` of complex shapes. -/
@[simps]
noncomputable def stupidTruncFunctor [e.IsRelIff] :
    HomologicalComplex C c' â¥¤ HomologicalComplex C c' where
  obj K := K.stupidTrunc e
  map Ï† := HomologicalComplex.stupidTruncMap Ï† e

end ComplexShape.Embedding
