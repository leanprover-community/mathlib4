/-
Copyright (c) 2025 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
module

public import Mathlib.Algebra.Homology.SpectralSequence.Basic

/-!
# Edge maps

-/

@[expose] public section

namespace CategoryTheory

open Category Limits

variable {C : Type*} [Category* C] [Abelian C]
  {Î¹ : Type*} {c : â„¤ â†’ ComplexShape Î¹} {râ‚€ : â„¤}

namespace SpectralSequence

variable (E E' : SpectralSequence C c râ‚€)

/-- This means that the differential to an object E_r^{p,q} is zero (both r and (p,q) fixed) -/
class HasEdgeMonoAt (pq : Î¹) (r : â„¤) : Prop where
  le : râ‚€ â‰¤ r := by lia
  zero (pq' : Î¹) : (E.page r).d pq' pq = 0

lemma leâ‚€_of_hasEdgeMonoAt (pq : Î¹) (r : â„¤) [E.HasEdgeMonoAt pq r] :
    râ‚€ â‰¤ r :=
  HasEdgeMonoAt.le (E := E) (pq := pq)

@[simp]
lemma d_eq_zero_of_hasEdgeMonoAt (pq' pq : Î¹) (r : â„¤) [E.HasEdgeMonoAt pq r] :
    (E.page r (E.leâ‚€_of_hasEdgeMonoAt pq r)).d pq' pq = 0 :=
  HasEdgeMonoAt.zero _

noncomputable def edgeMonoStep (pq : Î¹) (r r' : â„¤) [E.HasEdgeMonoAt pq r]
    (hrr' : r + 1 = r' := by lia) :
  (E.page r' (by have := E.leâ‚€_of_hasEdgeMonoAt pq r; lia)).X pq âŸ¶
    (E.page r (E.leâ‚€_of_hasEdgeMonoAt pq r)).X pq :=
  (E.iso r r' pq hrr' (E.leâ‚€_of_hasEdgeMonoAt pq r)).inv â‰«
    ((E.page r (E.leâ‚€_of_hasEdgeMonoAt pq r)).isoHomologyÏ€ _ pq rfl (by simp)).inv â‰«
    (E.page r (E.leâ‚€_of_hasEdgeMonoAt pq r)).iCycles pq

instance (pq : Î¹) (r r' : â„¤) (hrr' : r + 1 = r') [E.HasEdgeMonoAt pq r] :
    Mono (E.edgeMonoStep pq r r' hrr') := by
  dsimp [edgeMonoStep]
  infer_instance

@[reassoc (attr := simp)]
lemma edgeMonoStep_comp_d (pq pq' : Î¹) (r r' : â„¤)
    (hrr' : r + 1 = r' := by lia)
    [E.HasEdgeMonoAt pq r] :
    E.edgeMonoStep pq r r' hrr' â‰« (E.page r (E.leâ‚€_of_hasEdgeMonoAt pq r)).d pq pq' = 0 := by
  simp [edgeMonoStep]

@[reassoc (attr := simp)]
lemma iso_hom_comp_edgeMonoStep (pq : Î¹) (r r' : â„¤) [E.HasEdgeMonoAt pq r]
    (hrr' : r + 1 = r' := by lia) :
    (E.iso r r' pq hrr' (E.leâ‚€_of_hasEdgeMonoAt pq r)).hom â‰« E.edgeMonoStep pq r r' =
      ((E.page r (E.leâ‚€_of_hasEdgeMonoAt pq r)).isoHomologyÏ€ _ pq rfl (by simp)).inv â‰«
        (E.page r (E.leâ‚€_of_hasEdgeMonoAt pq r)).iCycles pq := by
  simp [edgeMonoStep]

@[simps]
noncomputable def edgeMonoStepShortComplex (pq pq' : Î¹) (r r' : â„¤) [E.HasEdgeMonoAt pq r]
    (hr' : r + 1 = r' := by lia) : ShortComplex C :=
  ShortComplex.mk _ _ (E.edgeMonoStep_comp_d pq pq' r r')

lemma edgeMonoStepShortComplex_exact (pq pq' : Î¹) (r r' : â„¤) [E.HasEdgeMonoAt pq r]
    (hpq' : (c r).next pq = pq')
    (hrr' : r + 1 = r' := by lia) :
    (E.edgeMonoStepShortComplex pq pq' r r' hrr').Exact := by
  subst hpq'
  apply ShortComplex.exact_of_f_is_kernel
  refine IsLimit.ofIsoLimit (((E.page r (E.leâ‚€_of_hasEdgeMonoAt pq r)).sc pq).cyclesIsKernel) ?_
  exact (Fork.ext (((E.page r (E.leâ‚€_of_hasEdgeMonoAt pq r)).isoHomologyÏ€ _ pq rfl (by simp)) â‰ªâ‰«
    E.iso r r' pq hrr' (E.leâ‚€_of_hasEdgeMonoAt pq r)) (by simp; rfl))

/-- This means that the differential from an object E_r^{p,q} is zero (both r and (p,q) fixed) -/
class HasEdgeEpiAt (pq : Î¹) (r : â„¤) : Prop where
  le : râ‚€ â‰¤ r := by lia
  zero (pq' : Î¹) : (E.page r).d pq pq' = 0

lemma leâ‚€_of_hasEdgeEpiAt (pq : Î¹) (r : â„¤) [E.HasEdgeEpiAt pq r] :
    râ‚€ â‰¤ r :=
  HasEdgeEpiAt.le (E := E) (pq := pq)

@[simp]
lemma d_eq_zero_of_hasEdgeEpiAt (pq pq' : Î¹) (r : â„¤) [E.HasEdgeEpiAt pq r] :
    (E.page r (E.leâ‚€_of_hasEdgeEpiAt pq r)).d pq pq' = 0 :=
  HasEdgeEpiAt.zero _

noncomputable def edgeEpiStep (pq : Î¹) (r r' : â„¤) [E.HasEdgeEpiAt pq r]
    (hrr' : r + 1 = r' := by lia) :
  (E.page r (E.leâ‚€_of_hasEdgeEpiAt pq r)).X pq âŸ¶ (E.page r'
    (by have := E.leâ‚€_of_hasEdgeEpiAt pq r; lia)).X pq :=
    (E.page r (E.leâ‚€_of_hasEdgeEpiAt pq r)).pOpcycles pq â‰«
      ((E.page r (E.leâ‚€_of_hasEdgeEpiAt pq r) ).isoHomologyÎ¹ pq _ rfl (by simp)).inv â‰«
      (E.iso r r' pq hrr' (E.leâ‚€_of_hasEdgeEpiAt pq r)).hom

instance (pq : Î¹) (r r' : â„¤) (hrr' : r + 1 = r') [E.HasEdgeEpiAt pq r] :
    Epi (E.edgeEpiStep pq r r' hrr') := by
  dsimp [edgeEpiStep]
  infer_instance

@[reassoc (attr := simp)]
lemma d_comp_edgeEpiStep (pq pq' : Î¹) (r r' : â„¤) [E.HasEdgeEpiAt pq' r]
    (hrr' : r + 1 = r' := by lia) :
    (E.page r (E.leâ‚€_of_hasEdgeEpiAt pq' r)).d pq pq' â‰« E.edgeEpiStep pq' r r' hrr' = 0 := by
  simp [edgeEpiStep]

@[reassoc (attr := simp)]
lemma edgeEpiStep_comp_iso_inv (pq : Î¹) (r r' : â„¤) [E.HasEdgeEpiAt pq r]
    (hrr' : r + 1 = r' := by lia) :
    E.edgeEpiStep pq r r' hrr' â‰« (E.iso r r' pq hrr' (E.leâ‚€_of_hasEdgeEpiAt pq r)).inv =
      (E.page r (E.leâ‚€_of_hasEdgeEpiAt pq r)).pOpcycles pq â‰«
        ((E.page r (E.leâ‚€_of_hasEdgeEpiAt pq r)).isoHomologyÎ¹ pq _ rfl (by simp)).inv := by
  simp [edgeEpiStep]

@[simps]
noncomputable def edgeEpiStepShortComplex (pq pq' : Î¹) (r r' : â„¤) [E.HasEdgeEpiAt pq' r]
    (hrr' : r + 1 = r' := by lia) : ShortComplex C :=
  ShortComplex.mk _ _ (E.d_comp_edgeEpiStep pq pq' r r' hrr')

lemma edgeEpiStepShortComplex_exact (pq pq' : Î¹) (r r' : â„¤) [E.HasEdgeEpiAt pq' r]
    (hpq' : (c r).prev pq' = pq) (hrr' : r + 1 = r' := by lia) :
    (E.edgeEpiStepShortComplex pq pq' r r' hrr').Exact := by
  subst hpq'
  apply ShortComplex.exact_of_g_is_cokernel
  refine IsColimit.ofIsoColimit
    (((E.page r (E.leâ‚€_of_hasEdgeEpiAt pq' r)).sc pq').opcyclesIsCokernel) (Iso.symm ?_)
  exact Cofork.ext ((E.iso r r' pq' hrr' (E.leâ‚€_of_hasEdgeEpiAt pq' r)).symm â‰ªâ‰«
    (E.page r (E.leâ‚€_of_hasEdgeEpiAt pq' r)).isoHomologyÎ¹ pq' _ rfl (by simp))
    (by simp; rfl)

@[reassoc (attr := simp)]
lemma edgeMonoStep_edgeEpiStep (pq : Î¹) (r r' : â„¤) [E.HasEdgeMonoAt pq r] [E.HasEdgeEpiAt pq r]
    (hrr' : r + 1 = r' := by lia) :
        E.edgeMonoStep pq r r' hrr' â‰« E.edgeEpiStep pq r r' hrr' = ðŸ™ _ := by
  simp only [edgeMonoStep, edgeEpiStep, assoc,
    â† (E.page r (E.leâ‚€_of_hasEdgeEpiAt pq r)).homology_Ï€_Î¹_assoc pq,
    HomologicalComplex.isoHomologyÎ¹_hom_inv_id_assoc,
    HomologicalComplex.isoHomologyÏ€_inv_hom_id_assoc, Iso.inv_hom_id]

@[reassoc (attr := simp)]
lemma edgeEpiStep_edgeMonoStep (pq : Î¹) (r r' : â„¤) [E.HasEdgeMonoAt pq r] [E.HasEdgeEpiAt pq r]
    (hrr' : r + 1 = r' := by lia) :
    E.edgeEpiStep pq r r' hrr' â‰« E.edgeMonoStep pq r r' hrr' = ðŸ™ _ := by
  have := E.leâ‚€_of_hasEdgeEpiAt pq r
  simp only [edgeEpiStep, edgeMonoStep,
    â† cancel_mono ((E.page r).isoHomologyÏ€ _ pq rfl (by simp)).hom,
    â† cancel_mono ((E.page r).isoHomologyÎ¹ pq _ rfl (by simp)).hom,
    â† cancel_mono ((E.page r).iCyclesIso pq _ rfl (by simp)).inv,
    HomologicalComplex.iCyclesIso_hom_inv_id, comp_id, id_comp,
    HomologicalComplex.isoHomologyÏ€_hom, HomologicalComplex.isoHomologyÏ€_inv_hom_id,
    HomologicalComplex.isoHomologyÎ¹_hom, HomologicalComplex.isoHomologyÎ¹_inv_hom_id,
    HomologicalComplex.homology_Ï€_Î¹, HomologicalComplex.iCyclesIso_inv_hom_id_assoc,
    assoc, Iso.hom_inv_id_assoc]

section

variable (r r' : â„¤) (pq pq' pq'' : Î¹)
  (hpq : (c r).prev pq' = pq) (hpq'' : (c r).next pq' = pq'')
  {hr : râ‚€ â‰¤ r}
  (left : ((E.page r hr).sc' pq pq' pq'').LeftHomologyData)
  (right : ((E.page r hr).sc' pq pq' pq'').RightHomologyData)

@[reassoc]
lemma leftHomologyData_Ï€_edgeMonoStep_compatibility [E.HasEdgeMonoAt pq' r]
    (hrr' : r + 1 = r' := by lia) :
    left.Ï€ â‰« left.homologyIso.inv â‰« ((E.page r).homologyIsoSc' _ _ _ hpq hpq'').inv â‰«
      (E.iso r r' pq' hrr').hom â‰« E.edgeMonoStep pq' r r' hrr' = left.i := by
  simp

@[reassoc (attr := simp)]
lemma rightHomologyData_p_edgeMonoStep_compatibility [E.HasEdgeMonoAt pq' r]
    (hrr' : r + 1 = r' := by lia) :
      E.edgeMonoStep pq' r r' hrr' â‰« right.p =
       (E.iso r r' pq' hrr' hr).inv â‰« ((E.page r).homologyIsoSc' _ _ _ hpq hpq'').hom â‰«
       right.homologyIso.hom â‰« right.Î¹ := by
  rw [â† cancel_epi (E.iso r r' pq').hom,
    â† cancel_epi ((E.page r).isoHomologyÏ€  _ pq' rfl (by apply d_eq_zero_of_hasEdgeMonoAt)).hom]
  simp [edgeMonoStep]

@[reassoc (attr := simp)]
lemma leftHomologyData_i_edgeEpiStep_compatibility [E.HasEdgeEpiAt pq' r]
    (hrr' : r + 1 = r' := by lia) :
    left.i â‰« E.edgeEpiStep pq' r r' hrr' =
      left.Ï€ â‰« left.homologyIso.inv â‰« ((E.page r).homologyIsoSc' _ _ _ hpq hpq'').inv â‰«
        (E.iso r r' pq' hrr' hr).hom := by
  rw [â† cancel_mono (E.iso r r' pq').inv,
    â† cancel_mono ((E.page r).isoHomologyÎ¹  pq' pq'' hpq''
      (by apply d_eq_zero_of_hasEdgeEpiAt)).hom]
  simp [edgeEpiStep]

@[reassoc]
lemma rightHomologyData_Î¹_edgeEpiStep_compatibility [E.HasEdgeEpiAt pq' r]
      (hrr' : r + 1 = r' := by lia) :
    E.edgeEpiStep pq' r r' hrr' â‰« (E.iso r r' pq' hrr' hr).inv â‰«
      ((E.page r).homologyIsoSc' _ _ _ hpq hpq'').hom â‰«
        right.homologyIso.hom â‰« right.Î¹ = right.p := by
  simp

end


def hasEdgeMonoSet (pq : Î¹) : Set â„¤  :=
  fun r => râ‚€ â‰¤ r âˆ§ âˆ€ (r' : â„¤) (_ : r â‰¤ r'), E.HasEdgeMonoAt pq r'

def hasEdgeEpiSet (pq : Î¹) : Set â„¤ :=
  fun r => râ‚€ â‰¤ r âˆ§ âˆ€ (r' : â„¤) (_ : r â‰¤ r'), E.HasEdgeEpiAt pq r'

section

variable {E E'}

lemma isIso_hom_succ (f : E âŸ¶ E') (r : â„¤) {hr : râ‚€ â‰¤ r}
    (hf : IsIso (f.hom r hr)) : IsIso (f.hom (r + 1)) := by
  have : âˆ€ (pq : Î¹), IsIso ((f.hom (r + 1)).f pq) := fun pq => by
    have : IsIso (HomologicalComplex.homologyMap (Hom.hom f r) pq) := by
      -- this should already be an instance
      change IsIso ((HomologicalComplex.homologyFunctor _ _ pq).map (Hom.hom f r))
      infer_instance
    exact IsIso.of_isIso_fac_left (Hom.comm f r (r + 1) pq).symm
  apply HomologicalComplex.Hom.isIso_of_components

lemma isIso_hom_of_GE (f : E âŸ¶ E') (r r' : â„¤) (hrr' : r â‰¤ r') {hr : râ‚€ â‰¤ r}
    (hf : IsIso (f.hom r hr)) : IsIso (f.hom r') := by
  obtain âŸ¨k, hkâŸ© := Int.le.dest hrr'
  revert r' hrr'
  induction k with
  | zero =>
    intro r' _ hrr'
    obtain rfl : r = r' := by lia
    exact hf
  | succ k hk =>
    intro r' _ hrr'
    obtain rfl : r' = (r + k) + 1 := by omega
    exact isIso_hom_succ f (r + k) (hk (r + k) (by lia) (by rfl))

end


end SpectralSequence

end CategoryTheory
