/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.Algebra.Homology.ShortComplex.HomologicalComplex
public import Mathlib.Algebra.Homology.ShortComplex.Abelian
public import Mathlib.Algebra.Homology.SpectralSequence.ComplexShape

/-!
# Spectral sequences

In this file, we define the category `SpectralSequence C c r‚ÇÄ` of spectral sequences
in an abelian category `C` with `E·µ£`-pages defined from `r‚ÇÄ : ‚Ñ§` having differentials
given by complex shapes `c : ‚Ñ§ ‚Üí ComplexShape Œπ`, where `Œπ` is the index type
for the objects on each page (e.g. `Œπ := ‚Ñ§ √ó ‚Ñ§` or `Œπ := ‚Ñï √ó ‚Ñï`).
A spectral sequence is defined as the data of a sequence of homological complexes
(the pages) and a sequence of isomorphism between the homology of a page and the
next page.

-/

@[expose] public section

namespace CategoryTheory

open Category Limits

variable (C : Type*) [Category C] [Abelian C]
  {Œ∫ : Type*} (c : ‚Ñ§ ‚Üí ComplexShape Œ∫) (r‚ÇÄ : ‚Ñ§)

/-- Given an abelian category `C`, a sequence of complex shapes `c : ‚Ñ§ ‚Üí ComplexShape Œπ`
and a starting page `r‚ÇÄ : ‚Ñ§`, a spectral sequence involves pages which are homological
complexes and isomorphisms saying that the homology of a page identifies to the next page. -/
structure SpectralSequence where
  /-- the `r`th page of a spectral sequence is an homological complex -/
  page (r : ‚Ñ§) (hr : r‚ÇÄ ‚â§ r := by lia) : HomologicalComplex C (c r)
  /-- the isomorphism between the homology of the `r`-th page at an object `pq : Œπ`
  and the corresponding object on the next page -/
  iso (r r' : ‚Ñ§) (pq : Œ∫) (hrr' : r + 1 = r' := by lia) (hr : r‚ÇÄ ‚â§ r := by lia) :
    (page r).homology pq ‚âÖ (page r').X pq

namespace SpectralSequence

variable {C c r‚ÇÄ}

/-- A morphism of spectral sequence is a sequence of morphisms between the
pages which commutes with the isomorphisms in homology. -/
@[ext]
structure Hom (E E' : SpectralSequence C c r‚ÇÄ) where
  /-- the morphism of homological complexes between the `r`th pages -/
  hom (r : ‚Ñ§) (hr : r‚ÇÄ ‚â§ r := by lia) : E.page r ‚ü∂ E'.page r
  comm (r r' : ‚Ñ§) (pq : Œ∫) (hrr' : r + 1 = r' := by lia) (hr : r‚ÇÄ ‚â§ r := by lia) :
    HomologicalComplex.homologyMap (hom r) pq ‚â´ (E'.iso r r' pq).hom =
      (E.iso r r' pq).hom ‚â´ (hom r').f pq := by cat_disch

/-- If `E` is a spectral sequence, and `r = r'`, this is the
isomorphism `(E.page r).X pq ‚âÖ (E.page r').X pq`. -/
def pageXIsoOfEq (E : SpectralSequence C c r‚ÇÄ) (pq : Œ∫) (r r' : ‚Ñ§) (h : r = r' := by lia)
    (hr : r‚ÇÄ ‚â§ r := by lia) :
    (E.page r).X pq ‚âÖ (E.page r').X pq :=
  eqToIso (by subst h; rfl)

attribute [reassoc (attr := simp)] Hom.comm

@[simps! id_hom comp_hom]
instance : Category (SpectralSequence C c r‚ÇÄ) where
  Hom := Hom
  id _ := { hom _ _ := ùüô _}
  comp f g :=
    { hom r hr := f.hom r ‚â´ g.hom r
      comm r r' hrr' pq hr := by
        dsimp
        simp [HomologicalComplex.homologyMap_comp, assoc, g.comm r r', f.comm_assoc r r'] }

@[ext]
lemma hom_ext {E E' : SpectralSequence C c r‚ÇÄ} {f f' : E ‚ü∂ E'}
    (h : ‚àÄ (r : ‚Ñ§) (hr : r‚ÇÄ ‚â§ r), f.hom r = f'.hom r) :
    f = f' :=
  Hom.ext (by grind)

attribute [simp] id_hom
attribute [reassoc, simp] comp_hom

variable (C c r‚ÇÄ)

/-- The functor `SpectralSequence C c r‚ÇÄ ‚•§ HomologicalComplex C (c r)` which
sends a spectral sequence to its `r`th page. -/
@[simps]
def pageFunctor (r : ‚Ñ§) (hr : r‚ÇÄ ‚â§ r := by lia) :
    SpectralSequence C c r‚ÇÄ ‚•§ HomologicalComplex C (c r) where
  obj E := E.page r
  map f := f.hom r

/-- The natural isomorphism between the homology of a spectral sequence on the
object `pq : Œπ` of the `r`th page and the corresponding object on the next page. -/
@[simps!]
noncomputable def pageHomologyNatIso
    (r r' : ‚Ñ§) (pq : Œ∫) (hrr' : r + 1 = r' := by lia) (hr : r‚ÇÄ ‚â§ r := by lia) :
    pageFunctor C c r‚ÇÄ r ‚ãô HomologicalComplex.homologyFunctor _ _ pq ‚âÖ
      pageFunctor C c r‚ÇÄ r' ‚ãô HomologicalComplex.eval _ _ pq :=
  NatIso.ofComponents (fun E ‚Ü¶ E.iso r r' pq)

end SpectralSequence

/-- A cohomological spectral sequence has differentials given by the
vector `(r, 1 - r)` on the `r`th page. -/
abbrev CohomologicalSpectralSequence :=
  SpectralSequence C (fun r ‚Ü¶ ComplexShape.up' (‚ü®r, 1 - r‚ü© : ‚Ñ§ √ó ‚Ñ§))

/-- A `E‚ÇÇ`-cohomological spectral sequence has differentials given by the
vector `(r, 1 - r)` on the `r`th page for `2 ‚â§ r`. -/
abbrev E‚ÇÇCohomologicalSpectralSequence := CohomologicalSpectralSequence C 2

/-- A first quadrant cohomological spectral sequence has differentials
given by the vector `(r, 1 - r)` on the `r`th page. -/
abbrev CohomologicalSpectralSequenceNat :=
  SpectralSequence C (fun r ‚Ü¶ ComplexShape.spectralSequenceNat ‚ü®r, 1 - r‚ü©)

/-- A first quadrant `E‚ÇÇ`-cohomological spectral sequence has differentials
given by the vector `(r, 1 - r)` on the `r`th page for `2 ‚â§ r`. -/
abbrev E‚ÇÇCohomologicalSpectralSequenceNat :=
  CohomologicalSpectralSequenceNat C 2

/-- A cohomological spectral sequence lying on finitely many rows
has differentials given by the vector `(r, 1 - r)` on the `r`th page. -/
abbrev CohomologicalSpectralSequenceFin (l : ‚Ñï) :=
  SpectralSequence C (fun r ‚Ü¶ ComplexShape.spectralSequenceFin l ‚ü®r, 1 - r‚ü©)

/-- A `E‚ÇÇ`-cohomological spectral sequence lying on finitely many rows
has differentials given by the vector `(r, 1 - r)` on the `r`th page for `2 ‚â§ r`. -/
abbrev E‚ÇÇCohomologicalSpectralSequenceFin (l : ‚Ñï) :=
  CohomologicalSpectralSequenceFin C 2 l

end CategoryTheory
