/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.Algebra.Homology.ShortComplex.HomologicalComplex
public import Mathlib.Algebra.Homology.ShortComplex.Abelian

/-!
# Spectral sequences

-/

@[expose] public section

namespace ComplexShape

def spectralSequenceNat (u : ‚Ñ§ √ó ‚Ñ§) : ComplexShape (‚Ñï √ó ‚Ñï) where
  Rel a b := a.1 + u.1 = b.1 ‚àß a.2 + u.2 = b.2
  next_eq {a b b'} := by
    rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚ü®h‚ÇÉ, h‚ÇÑ‚ü©
    ext <;> lia
  prev_eq {a a' b} := by
    rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚ü®h‚ÇÉ, h‚ÇÑ‚ü©
    ext <;> lia

instance (u : ‚Ñ§ √ó ‚Ñ§) : DecidableRel (spectralSequenceNat u).Rel := fun a b => by
  dsimp [spectralSequenceNat]
  infer_instance

@[simp]
lemma spectralSequenceNat_rel_iff (u : ‚Ñ§ √ó ‚Ñ§) (a b : ‚Ñï √ó ‚Ñï) :
    (spectralSequenceNat u).Rel a b ‚Üî a.1 + u.1 = b.1 ‚àß a.2 + u.2 = b.2 := by rfl

def spectralSequenceFin (l : ‚Ñï) (u : ‚Ñ§ √ó ‚Ñ§) : ComplexShape (‚Ñ§ √ó Fin l) where
  Rel a b := a.1 + u.1 = b.1 ‚àß a.2.1 + u.2 = b.2.1
  next_eq := by
    rintro ‚ü®a‚ÇÅ, ‚ü®a‚ÇÇ, _‚ü©‚ü© ‚ü®b‚ÇÅ, ‚ü®b‚ÇÇ, _‚ü©‚ü©‚ü®b‚ÇÅ', ‚ü®b‚ÇÇ', _‚ü©‚ü© ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚ü®h‚ÇÉ, h‚ÇÑ‚ü©
    ext <;> lia
  prev_eq := by
    rintro ‚ü®a‚ÇÅ, ‚ü®a‚ÇÇ, _‚ü©‚ü© ‚ü®a‚ÇÅ', ‚ü®a‚ÇÇ', _‚ü©‚ü©‚ü®b‚ÇÅ, ‚ü®b‚ÇÇ, _‚ü©‚ü© ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚ü®h‚ÇÉ, h‚ÇÑ‚ü©
    ext <;> lia

end ComplexShape

namespace CategoryTheory

open Category Limits

variable (C : Type*) [Category C] [Abelian C]
  {Œπ : Type*} (c : ‚Ñ§ ‚Üí ComplexShape Œπ) (r‚ÇÄ : ‚Ñ§)

structure SpectralSequence where
  page (r : ‚Ñ§) (hr : r‚ÇÄ ‚â§ r := by lia) : HomologicalComplex C (c r)
  iso (r r' : ‚Ñ§) (pq : Œπ) (hrr' : r + 1 = r' := by lia) (hr : r‚ÇÄ ‚â§ r := by lia) :
    (page r).homology pq ‚âÖ (page r').X pq

namespace SpectralSequence

variable {C c r‚ÇÄ}
variable (E E' E'' : SpectralSequence C c r‚ÇÄ)

@[ext]
structure Hom where
  hom (r : ‚Ñ§) (hr : r‚ÇÄ ‚â§ r := by lia) : E.page r ‚ü∂ E'.page r
  comm (r r' : ‚Ñ§) (pq : Œπ) (hrr' : r + 1 = r' := by lia) (hr : r‚ÇÄ ‚â§ r := by lia) :
    HomologicalComplex.homologyMap (hom r) pq ‚â´ (E'.iso r r' pq).hom =
      (E.iso r r' pq).hom ‚â´ (hom r').f pq := by aesop_cat

def pageXIsoOfEq (pq : Œπ) (r r' : ‚Ñ§) (h : r = r') (hr : r‚ÇÄ ‚â§ r := by lia) :
    (E.page r).X pq ‚âÖ (E.page r').X pq :=
  eqToIso (by subst h; rfl)

namespace Hom

attribute [reassoc] comm

@[simps]
def id : Hom E E where
  hom r hr := ùüô _

variable {E E' E''}

@[simps]
def comp (f : Hom E E') (g : Hom E' E'') : Hom E E'' where
  hom r hr := f.hom r ‚â´ g.hom r
  comm r r' hrr' pq hr := by
    dsimp
    rw [HomologicalComplex.homologyMap_comp, assoc, g.comm r r', f.comm_assoc r r']

end Hom

instance : Category (SpectralSequence C c r‚ÇÄ) where
  Hom := Hom
  id := Hom.id
  comp := Hom.comp

variable {E E'}

lemma hom_ext {f f' : E ‚ü∂ E'}
    (h : ‚àÄ (r : ‚Ñ§) (hr : r‚ÇÄ ‚â§ r), f.hom r = f'.hom r) :
    f = f' := by
  apply Hom.ext
  ext r hr : 2
  exact h r hr

variable (E)

@[simp]
lemma id_hom (r : ‚Ñï) (hr : r‚ÇÄ ‚â§ r := by lia) :
    Hom.hom (ùüô E) r = ùüô _ := rfl

variable {E E''}

@[reassoc, simp]
lemma comp_hom (f : E ‚ü∂ E') (g : E' ‚ü∂ E'') (r : ‚Ñï) (hr : r‚ÇÄ ‚â§ r := by lia) :
    (f ‚â´ g).hom r = f.hom r ‚â´ g.hom r := rfl

variable (C c r‚ÇÄ)

@[simps]
def pageFunctor (r : ‚Ñ§) (hr : r‚ÇÄ ‚â§ r := by lia) :
    SpectralSequence C c r‚ÇÄ ‚•§ HomologicalComplex C (c r) where
  obj E := E.page r
  map f := f.hom r

@[simps!]
noncomputable def pageHomologyNatIso
    (r r' : ‚Ñ§) (pq : Œπ) (hrr' : r + 1 = r' := by lia) (hr : r‚ÇÄ ‚â§ r := by lia) :
    pageFunctor C c r‚ÇÄ r ‚ãô HomologicalComplex.homologyFunctor _ _ pq ‚âÖ
      pageFunctor C c r‚ÇÄ r' ‚ãô HomologicalComplex.eval _ _ pq :=
  NatIso.ofComponents (fun E => E.iso r r' pq) (fun _ ‚Ü¶ Hom.comm _ _ _ _ (by lia))

variable {C c r‚ÇÄ}

/-- This means that the differential to an object E_r^{p,q} is zero (both r and (p,q) fixed) -/
class HasEdgeMonoAt (pq : Œπ) (r : ‚Ñ§) : Prop where
  le : r‚ÇÄ ‚â§ r := by lia
  zero (pq' : Œπ) : (E.page r).d pq' pq = 0

lemma le‚ÇÄ_of_hasEdgeMonoAt (pq : Œπ) (r : ‚Ñ§) [E.HasEdgeMonoAt pq r] :
    r‚ÇÄ ‚â§ r :=
  HasEdgeMonoAt.le (E := E) (pq := pq)

@[simp]
lemma d_eq_zero_of_hasEdgeMonoAt (pq' pq : Œπ) (r : ‚Ñ§) [E.HasEdgeMonoAt pq r] :
    (E.page r (E.le‚ÇÄ_of_hasEdgeMonoAt pq r)).d pq' pq = 0 :=
  HasEdgeMonoAt.zero _

noncomputable def edgeMonoStep (pq : Œπ) (r r' : ‚Ñ§) [E.HasEdgeMonoAt pq r]
    (hrr' : r + 1 = r' := by lia) :
  (E.page r' (by have := E.le‚ÇÄ_of_hasEdgeMonoAt pq r; lia)).X pq ‚ü∂
    (E.page r (E.le‚ÇÄ_of_hasEdgeMonoAt pq r)).X pq :=
  (E.iso r r' pq hrr' (E.le‚ÇÄ_of_hasEdgeMonoAt pq r)).inv ‚â´
    ((E.page r (E.le‚ÇÄ_of_hasEdgeMonoAt pq r)).isoHomologyœÄ _ pq rfl (by simp)).inv ‚â´
    (E.page r (E.le‚ÇÄ_of_hasEdgeMonoAt pq r)).iCycles pq

instance (pq : Œπ) (r r' : ‚Ñ§) (hrr' : r + 1 = r') [E.HasEdgeMonoAt pq r] :
    Mono (E.edgeMonoStep pq r r' hrr') := by
  dsimp [edgeMonoStep]
  infer_instance

@[reassoc (attr := simp)]
lemma edgeMonoStep_comp_d (pq pq' : Œπ) (r r' : ‚Ñ§)
    (hrr' : r + 1 = r' := by lia)
    [E.HasEdgeMonoAt pq r] :
    E.edgeMonoStep pq r r' hrr' ‚â´ (E.page r (E.le‚ÇÄ_of_hasEdgeMonoAt pq r)).d pq pq' = 0 := by
  simp [edgeMonoStep]

@[reassoc (attr := simp)]
lemma iso_hom_comp_edgeMonoStep (pq : Œπ) (r r' : ‚Ñ§) [E.HasEdgeMonoAt pq r]
    (hrr' : r + 1 = r' := by lia) :
    (E.iso r r' pq hrr' (E.le‚ÇÄ_of_hasEdgeMonoAt pq r)).hom ‚â´ E.edgeMonoStep pq r r' =
      ((E.page r (E.le‚ÇÄ_of_hasEdgeMonoAt pq r)).isoHomologyœÄ _ pq rfl (by simp)).inv ‚â´
        (E.page r (E.le‚ÇÄ_of_hasEdgeMonoAt pq r)).iCycles pq := by
  simp [edgeMonoStep]

@[simps]
noncomputable def edgeMonoStepShortComplex (pq pq' : Œπ) (r r' : ‚Ñ§) [E.HasEdgeMonoAt pq r]
    (hr' : r + 1 = r' := by lia) : ShortComplex C :=
  ShortComplex.mk _ _ (E.edgeMonoStep_comp_d pq pq' r r')

lemma edgeMonoStepShortComplex_exact (pq pq' : Œπ) (r r' : ‚Ñ§) [E.HasEdgeMonoAt pq r]
    (hpq' : (c r).next pq = pq')
    (hrr' : r + 1 = r' := by lia) :
    (E.edgeMonoStepShortComplex pq pq' r r' hrr').Exact := by
  subst hpq'
  apply ShortComplex.exact_of_f_is_kernel
  refine IsLimit.ofIsoLimit (((E.page r (E.le‚ÇÄ_of_hasEdgeMonoAt pq r)).sc pq).cyclesIsKernel) ?_
  exact (Fork.ext (((E.page r (E.le‚ÇÄ_of_hasEdgeMonoAt pq r)).isoHomologyœÄ _ pq rfl (by simp)) ‚â™‚â´
    E.iso r r' pq hrr' (E.le‚ÇÄ_of_hasEdgeMonoAt pq r)) (by simp; rfl))

/-- This means that the differential from an object E_r^{p,q} is zero (both r and (p,q) fixed) -/
class HasEdgeEpiAt (pq : Œπ) (r : ‚Ñ§) : Prop where
  le : r‚ÇÄ ‚â§ r := by lia
  zero (pq' : Œπ) : (E.page r).d pq pq' = 0

lemma le‚ÇÄ_of_hasEdgeEpiAt (pq : Œπ) (r : ‚Ñ§) [E.HasEdgeEpiAt pq r] :
    r‚ÇÄ ‚â§ r :=
  HasEdgeEpiAt.le (E := E) (pq := pq)

@[simp]
lemma d_eq_zero_of_hasEdgeEpiAt (pq pq' : Œπ) (r : ‚Ñ§) [E.HasEdgeEpiAt pq r] :
    (E.page r (E.le‚ÇÄ_of_hasEdgeEpiAt pq r)).d pq pq' = 0 :=
  HasEdgeEpiAt.zero _

noncomputable def edgeEpiStep (pq : Œπ) (r r' : ‚Ñ§) [E.HasEdgeEpiAt pq r]
    (hrr' : r + 1 = r' := by lia) :
  (E.page r (E.le‚ÇÄ_of_hasEdgeEpiAt pq r)).X pq ‚ü∂ (E.page r'
    (by have := E.le‚ÇÄ_of_hasEdgeEpiAt pq r; lia)).X pq :=
    (E.page r (E.le‚ÇÄ_of_hasEdgeEpiAt pq r)).pOpcycles pq ‚â´
      ((E.page r (E.le‚ÇÄ_of_hasEdgeEpiAt pq r) ).isoHomologyŒπ pq _ rfl (by simp)).inv ‚â´
      (E.iso r r' pq hrr' (E.le‚ÇÄ_of_hasEdgeEpiAt pq r)).hom

instance (pq : Œπ) (r r' : ‚Ñ§) (hrr' : r + 1 = r') [E.HasEdgeEpiAt pq r] :
    Epi (E.edgeEpiStep pq r r' hrr') := by
  dsimp [edgeEpiStep]
  infer_instance

@[reassoc (attr := simp)]
lemma d_comp_edgeEpiStep (pq pq' : Œπ) (r r' : ‚Ñ§) [E.HasEdgeEpiAt pq' r]
    (hrr' : r + 1 = r' := by lia) :
    (E.page r (E.le‚ÇÄ_of_hasEdgeEpiAt pq' r)).d pq pq' ‚â´ E.edgeEpiStep pq' r r' hrr' = 0 := by
  simp [edgeEpiStep]

@[reassoc (attr := simp)]
lemma edgeEpiStep_comp_iso_inv (pq : Œπ) (r r' : ‚Ñ§) [E.HasEdgeEpiAt pq r]
    (hrr' : r + 1 = r' := by lia) :
    E.edgeEpiStep pq r r' hrr' ‚â´ (E.iso r r' pq hrr' (E.le‚ÇÄ_of_hasEdgeEpiAt pq r)).inv =
      (E.page r (E.le‚ÇÄ_of_hasEdgeEpiAt pq r)).pOpcycles pq ‚â´
        ((E.page r (E.le‚ÇÄ_of_hasEdgeEpiAt pq r)).isoHomologyŒπ pq _ rfl (by simp)).inv := by
  simp [edgeEpiStep]

@[simps]
noncomputable def edgeEpiStepShortComplex (pq pq' : Œπ) (r r' : ‚Ñ§) [E.HasEdgeEpiAt pq' r]
    (hrr' : r + 1 = r' := by lia) : ShortComplex C :=
  ShortComplex.mk _ _ (E.d_comp_edgeEpiStep pq pq' r r' hrr')

lemma edgeEpiStepShortComplex_exact (pq pq' : Œπ) (r r' : ‚Ñ§) [E.HasEdgeEpiAt pq' r]
    (hpq' : (c r).prev pq' = pq) (hrr' : r + 1 = r' := by lia) :
    (E.edgeEpiStepShortComplex pq pq' r r' hrr').Exact := by
  subst hpq'
  apply ShortComplex.exact_of_g_is_cokernel
  refine IsColimit.ofIsoColimit
    (((E.page r (E.le‚ÇÄ_of_hasEdgeEpiAt pq' r)).sc pq').opcyclesIsCokernel) (Iso.symm ?_)
  exact Cofork.ext ((E.iso r r' pq' hrr' (E.le‚ÇÄ_of_hasEdgeEpiAt pq' r)).symm ‚â™‚â´
    (E.page r (E.le‚ÇÄ_of_hasEdgeEpiAt pq' r)).isoHomologyŒπ pq' _ rfl (by simp))
    (by simp; rfl)

@[reassoc (attr := simp)]
lemma edgeMonoStep_edgeEpiStep (pq : Œπ) (r r' : ‚Ñ§) [E.HasEdgeMonoAt pq r] [E.HasEdgeEpiAt pq r]
    (hrr' : r + 1 = r' := by lia) :
        E.edgeMonoStep pq r r' hrr' ‚â´ E.edgeEpiStep pq r r' hrr' = ùüô _ := by
  simp only [edgeMonoStep, edgeEpiStep, assoc,
    ‚Üê (E.page r (E.le‚ÇÄ_of_hasEdgeEpiAt pq r)).homology_œÄ_Œπ_assoc pq,
    HomologicalComplex.isoHomologyŒπ_hom_inv_id_assoc,
    HomologicalComplex.isoHomologyœÄ_inv_hom_id_assoc, Iso.inv_hom_id]

@[reassoc (attr := simp)]
lemma edgeEpiStep_edgeMonoStep (pq : Œπ) (r r' : ‚Ñ§) [E.HasEdgeMonoAt pq r] [E.HasEdgeEpiAt pq r]
    (hrr' : r + 1 = r' := by lia) :
    E.edgeEpiStep pq r r' hrr' ‚â´ E.edgeMonoStep pq r r' hrr' = ùüô _ := by
  have := E.le‚ÇÄ_of_hasEdgeEpiAt pq r
  simp only [edgeEpiStep, edgeMonoStep,
    ‚Üê cancel_mono ((E.page r).isoHomologyœÄ _ pq rfl (by simp)).hom,
    ‚Üê cancel_mono ((E.page r).isoHomologyŒπ pq _ rfl (by simp)).hom,
    ‚Üê cancel_mono ((E.page r).iCyclesIso pq _ rfl (by simp)).inv,
    HomologicalComplex.iCyclesIso_hom_inv_id, comp_id, id_comp,
    HomologicalComplex.isoHomologyœÄ_hom, HomologicalComplex.isoHomologyœÄ_inv_hom_id,
    HomologicalComplex.isoHomologyŒπ_hom, HomologicalComplex.isoHomologyŒπ_inv_hom_id,
    HomologicalComplex.homology_œÄ_Œπ, HomologicalComplex.iCyclesIso_inv_hom_id_assoc,
    assoc, Iso.hom_inv_id_assoc]

section

variable (r r' : ‚Ñ§) (pq pq' pq'' : Œπ)
  (hpq : (c r).prev pq' = pq) (hpq'' : (c r).next pq' = pq'')
  {hr : r‚ÇÄ ‚â§ r}
  (left : ((E.page r hr).sc' pq pq' pq'').LeftHomologyData)
  (right : ((E.page r hr).sc' pq pq' pq'').RightHomologyData)

@[reassoc]
lemma leftHomologyData_œÄ_edgeMonoStep_compatibility [E.HasEdgeMonoAt pq' r]
    (hrr' : r + 1 = r' := by lia) :
    left.œÄ ‚â´ left.homologyIso.inv ‚â´ ((E.page r).homologyIsoSc' _ _ _ hpq hpq'').inv ‚â´
      (E.iso r r' pq' hrr').hom ‚â´ E.edgeMonoStep pq' r r' hrr' = left.i := by
  simp

@[reassoc (attr := simp)]
lemma rightHomologyData_p_edgeMonoStep_compatibility [E.HasEdgeMonoAt pq' r]
    (hrr' : r + 1 = r' := by lia) :
      E.edgeMonoStep pq' r r' hrr' ‚â´ right.p =
       (E.iso r r' pq' hrr' hr).inv ‚â´ ((E.page r).homologyIsoSc' _ _ _ hpq hpq'').hom ‚â´
       right.homologyIso.hom ‚â´ right.Œπ := by
  rw [‚Üê cancel_epi (E.iso r r' pq').hom,
    ‚Üê cancel_epi ((E.page r).isoHomologyœÄ  _ pq' rfl (by apply d_eq_zero_of_hasEdgeMonoAt)).hom]
  simp [edgeMonoStep]

@[reassoc (attr := simp)]
lemma leftHomologyData_i_edgeEpiStep_compatibility [E.HasEdgeEpiAt pq' r]
    (hrr' : r + 1 = r' := by lia) :
    left.i ‚â´ E.edgeEpiStep pq' r r' hrr' =
      left.œÄ ‚â´ left.homologyIso.inv ‚â´ ((E.page r).homologyIsoSc' _ _ _ hpq hpq'').inv ‚â´
        (E.iso r r' pq' hrr' hr).hom := by
  rw [‚Üê cancel_mono (E.iso r r' pq').inv,
    ‚Üê cancel_mono ((E.page r).isoHomologyŒπ  pq' pq'' hpq''
      (by apply d_eq_zero_of_hasEdgeEpiAt)).hom]
  simp [edgeEpiStep]

@[reassoc]
lemma rightHomologyData_Œπ_edgeEpiStep_compatibility [E.HasEdgeEpiAt pq' r]
      (hrr' : r + 1 = r' := by lia) :
    E.edgeEpiStep pq' r r' hrr' ‚â´ (E.iso r r' pq' hrr' hr).inv ‚â´
      ((E.page r).homologyIsoSc' _ _ _ hpq hpq'').hom ‚â´
        right.homologyIso.hom ‚â´ right.Œπ = right.p := by
  simp

end


def hasEdgeMonoSet (pq : Œπ) : Set ‚Ñ§  :=
  fun r => r‚ÇÄ ‚â§ r ‚àß ‚àÄ (r' : ‚Ñ§) (_ : r ‚â§ r'), E.HasEdgeMonoAt pq r'

def hasEdgeEpiSet (pq : Œπ) : Set ‚Ñ§ :=
  fun r => r‚ÇÄ ‚â§ r ‚àß ‚àÄ (r' : ‚Ñ§) (_ : r ‚â§ r'), E.HasEdgeEpiAt pq r'

section

lemma isIso_hom_succ (f : E ‚ü∂ E') (r : ‚Ñ§) {hr : r‚ÇÄ ‚â§ r}
    (hf : IsIso (f.hom r hr)) : IsIso (f.hom (r + 1)) := by
  have : ‚àÄ (pq : Œπ), IsIso ((f.hom (r + 1)).f pq) := fun pq => by
    have : IsIso (HomologicalComplex.homologyMap (Hom.hom f r) pq) := by
      -- this should already be an instance
      change IsIso ((HomologicalComplex.homologyFunctor _ _ pq).map (Hom.hom f r))
      infer_instance
    exact IsIso.of_isIso_fac_left (Hom.comm f r (r + 1) pq).symm
  apply HomologicalComplex.Hom.isIso_of_components

lemma isIso_hom_of_GE (f : E ‚ü∂ E') (r r' : ‚Ñ§) (hrr' : r ‚â§ r') {hr : r‚ÇÄ ‚â§ r}
    (hf : IsIso (f.hom r hr)) : IsIso (f.hom r') := by
  obtain ‚ü®k, hk‚ü© := Int.le.dest hrr'
  revert r' hrr'
  induction k with
  | zero =>
    intro r' _ hrr'
    obtain rfl : r = r' := by lia
    exact hf
  | succ k hk =>
    intro r' _ hrr'
    obtain rfl : r' = (r + k) + 1 := by omega
    exact isIso_hom_succ f (r + k) (hk (r + k) (by lia) (by rfl))

end

end SpectralSequence

abbrev CohomologicalSpectralSequence :=
  SpectralSequence C (fun r => ComplexShape.up' (‚ü®r, 1 - r‚ü© : ‚Ñ§ √ó ‚Ñ§))

abbrev E‚ÇÇCohomologicalSpectralSequence := CohomologicalSpectralSequence C 2

abbrev CohomologicalSpectralSequenceNat :=
  SpectralSequence C (fun r => ComplexShape.spectralSequenceNat ‚ü®r, 1 - r‚ü©)

abbrev E‚ÇÇCohomologicalSpectralSequenceNat :=
  CohomologicalSpectralSequenceNat C 2

abbrev CohomologicalSpectralSequenceFin (l : ‚Ñï) :=
  SpectralSequence C (fun r => ComplexShape.spectralSequenceFin l ‚ü®r, 1 - r‚ü©)

abbrev E‚ÇÇCohomologicalSpectralSequenceFin (l : ‚Ñï) :=
  CohomologicalSpectralSequenceFin C 2 l

end CategoryTheory
