import Mathlib.Algebra.Homology.ShortComplex.HomologicalComplex
import Mathlib.Algebra.Homology.ShortComplex.Abelian
import Mathlib.Tactic.Linarith

lemma Int.eq_add_ofNat_of_le {i j : ‚Ñ§} (hij : i ‚â§ j) :
    ‚àÉ (d : ‚Ñï), j = i + d := by
  have h : 0 ‚â§ j - i := by linarith
  obtain ‚ü®d, hd‚ü© := Int.eq_ofNat_of_zero_le h
  exact ‚ü®d, by linarith‚ü©

namespace ComplexShape

def spectralSequenceNat (u : ‚Ñ§ √ó ‚Ñ§) : ComplexShape (‚Ñï √ó ‚Ñï) where
  Rel a b := a.1 + u.1 = b.1 ‚àß a.2 + u.2 = b.2
  next_eq {a b b'} := by
    rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚ü®h‚ÇÉ, h‚ÇÑ‚ü©
    ext <;> linarith
  prev_eq {a a' b} := by
    rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚ü®h‚ÇÉ, h‚ÇÑ‚ü©
    ext <;> linarith

instance (u : ‚Ñ§ √ó ‚Ñ§) : DecidableRel (spectralSequenceNat u).Rel := fun a b => by
  dsimp [spectralSequenceNat]
  infer_instance

@[simp]
lemma spectralSequenceNat_rel_iff (u : ‚Ñ§ √ó ‚Ñ§) (a b : ‚Ñï √ó ‚Ñï) :
    (spectralSequenceNat u).Rel a b ‚Üî a.1 + u.1 = b.1 ‚àß a.2 + u.2 = b.2 := by rfl

def spectralSequenceFin (l : ‚Ñï) (u : ‚Ñ§ √ó ‚Ñ§) : ComplexShape (‚Ñ§ √ó Fin l) where
  Rel a b := a.1 + u.1 = b.1 ‚àß a.2.1 + u.2 = b.2.1
  next_eq := by
    rintro ‚ü®a‚ÇÅ, ‚ü®a‚ÇÇ, _‚ü©‚ü© ‚ü®b‚ÇÅ, ‚ü®b‚ÇÇ, _‚ü©‚ü©‚ü®b‚ÇÅ', ‚ü®b‚ÇÇ', _‚ü©‚ü© ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚ü®h‚ÇÉ, h‚ÇÑ‚ü©
    ext <;> linarith
  prev_eq := by
    rintro ‚ü®a‚ÇÅ, ‚ü®a‚ÇÇ, _‚ü©‚ü© ‚ü®a‚ÇÅ', ‚ü®a‚ÇÇ', _‚ü©‚ü©‚ü®b‚ÇÅ, ‚ü®b‚ÇÇ, _‚ü©‚ü© ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚ü®h‚ÇÉ, h‚ÇÑ‚ü©
    ext <;> linarith

end ComplexShape

namespace CategoryTheory

open Category Limits

variable (C : Type*) [Category C] [Abelian C]
  {Œπ : Type*} (c : ‚Ñ§ ‚Üí ComplexShape Œπ) (r‚ÇÄ : ‚Ñ§)

structure SpectralSequence where
  page' (r : ‚Ñ§) (hr : r‚ÇÄ ‚â§ r) : HomologicalComplex C (c r)
  iso' (r r' : ‚Ñ§) (hrr' : r + 1 = r') (pq : Œπ) (hr : r‚ÇÄ ‚â§ r) :
    (page' r hr).homology pq ‚âÖ (page' r' (by linarith)).X pq

namespace SpectralSequence

variable {C c r‚ÇÄ}
variable (E E' E'' : SpectralSequence C c r‚ÇÄ)

@[ext]
structure Hom where
  hom' (r : ‚Ñ§) (hr : r‚ÇÄ ‚â§ r) : E.page' r hr ‚ü∂ E'.page' r (by linarith)
  comm' (r r' : ‚Ñ§) (hrr' : r + 1 = r') (pq : Œπ) (hr : r‚ÇÄ ‚â§ r) :
    HomologicalComplex.homologyMap (hom' r hr) pq ‚â´ (E'.iso' r r' hrr' pq hr).hom =
      (E.iso' r r' hrr' pq hr).hom ‚â´ (hom' r' (by linarith)).f pq := by aesop_cat

class HasPage (E : SpectralSequence C c r‚ÇÄ) (r : ‚Ñ§) : Prop where
  le : r‚ÇÄ ‚â§ r := by linarith

lemma le_of_hasPage (r : ‚Ñ§) [h : E.HasPage r] : r‚ÇÄ ‚â§ r := h.le

lemma hasPage_of_LE (r r' : ‚Ñ§) (le : r ‚â§ r') [E.HasPage r] : E.HasPage r' where
  le := by linarith [E.le_of_hasPage r]

instance : E.HasPage r‚ÇÄ where

instance [E.HasPage 0] : E.HasPage 1 := E.hasPage_of_LE 0 1 (by linarith)
instance [E.HasPage 1] : E.HasPage 2 := E.hasPage_of_LE 1 2 (by linarith)
instance [E.HasPage 2] : E.HasPage 3 := E.hasPage_of_LE 2 3 (by linarith)
instance [E.HasPage 3] : E.HasPage 4 := E.hasPage_of_LE 3 4 (by linarith)
instance [E.HasPage 4] : E.HasPage 5 := E.hasPage_of_LE 4 5 (by linarith)

instance (r : ‚Ñ§) [E.HasPage r] : E.HasPage (r + 1) :=
  E.hasPage_of_LE r _ (by linarith)

instance (r r' : ‚Ñ§) [E.HasPage r] : E.HasPage (max r r') :=
    E.hasPage_of_LE r _ (le_max_left _ _)

instance (r r' : ‚Ñ§) [E.HasPage r'] : E.HasPage (max r r') :=
    E.hasPage_of_LE r' _ (le_max_right _ _)

instance (r : ‚Ñ§) [E.HasPage r] (k : ‚Ñï) : E.HasPage (r + k) :=
  E.hasPage_of_LE r (r + k) (by linarith)

def page (r : ‚Ñ§) [E.HasPage r] :
    HomologicalComplex C (c r) :=
  E.page' r (E.le_of_hasPage r)

def iso (r r' : ‚Ñ§) (hrr' : r + 1 = r') (pq : Œπ) [E.HasPage r] [E.HasPage r'] :
    (E.page r).homology pq ‚âÖ (E.page r').X pq :=
  E.iso' r r' hrr' pq (E.le_of_hasPage r)

def pageXIsoOfEq (pq : Œπ) (r r' : ‚Ñ§) (h : r = r') [E.HasPage r] [E.HasPage r'] :
    (E.page r).X pq ‚âÖ (E.page r').X pq :=
  eqToIso (by subst h; rfl)

namespace Hom

attribute [reassoc] comm'

@[simps]
def id : Hom E E where
  hom' r hr := ùüô _

variable {E E' E''}

@[simps]
def comp (f : Hom E E') (g : Hom E' E'') : Hom E E'' where
  hom' r hr := f.hom' r hr ‚â´ g.hom' r hr
  comm' r r' hrr' pq hr := by
    dsimp
    rw [HomologicalComplex.homologyMap_comp, assoc, g.comm' r r', f.comm'_assoc r r']

def hom (f : Hom E E') (r : ‚Ñ§) [E.HasPage r] [E'.HasPage r] :
    E.page r ‚ü∂ E'.page r :=
  f.hom' r (E.le_of_hasPage r)

end Hom

instance : Category (SpectralSequence C c r‚ÇÄ) where
  Hom := Hom
  id := Hom.id
  comp := Hom.comp

variable {E E'}

lemma hom_ext {f f' : E ‚ü∂ E'} (h : ‚àÄ (r : ‚Ñ§) (_ : E.HasPage r) (_ : E'.HasPage r), f.hom r = f'.hom r) :
    f = f' := by
  apply Hom.ext
  ext r hr : 2
  exact h r ‚ü®hr‚ü© ‚ü®hr‚ü©

variable (E)

@[simp]
lemma id_hom (r : ‚Ñï) [E.HasPage r] :
    Hom.hom (ùüô E) r = ùüô _ := rfl

variable {E E''}

@[reassoc, simp]
lemma comp_hom (f : E ‚ü∂ E') (g : E' ‚ü∂ E'') (r : ‚Ñï) [E.HasPage r] [E'.HasPage r] [E''.HasPage r] :
    (f ‚â´ g).hom r = f.hom r ‚â´ g.hom r := rfl

@[reassoc (attr := simp)]
lemma Hom.comm (f : E ‚ü∂ E') (r r' : ‚Ñ§) (hrr' : r + 1 = r') (pq : Œπ)
    [E.HasPage r] [E'.HasPage r] [E.HasPage r'] [E'.HasPage r'] :
    HomologicalComplex.homologyMap (f.hom r) pq ‚â´ (E'.iso r r' hrr' pq).hom =
      (E.iso r r' hrr' pq).hom ‚â´ (f.hom r').f pq := by
  apply f.comm'

variable (C c r‚ÇÄ)

@[simps]
def pageFunctor (r : ‚Ñ§) [‚àÄ (E : SpectralSequence C c r‚ÇÄ), E.HasPage r] :
    SpectralSequence C c r‚ÇÄ ‚•§ HomologicalComplex C (c r) where
  obj E := E.page r
  map f := f.hom r

@[simps!]
noncomputable def pageHomologyNatIso
    (r r' : ‚Ñ§) (hrr' : r + 1 = r') (pq : Œπ)
    [‚àÄ (E : SpectralSequence C c r‚ÇÄ), E.HasPage r]
    [‚àÄ (E : SpectralSequence C c r‚ÇÄ), E.HasPage r'] :
    pageFunctor C c r‚ÇÄ r ‚ãô HomologicalComplex.homologyFunctor _ _ pq ‚âÖ
      pageFunctor C c r‚ÇÄ r' ‚ãô HomologicalComplex.eval _ _ pq :=
  NatIso.ofComponents (fun E => E.iso r r' hrr' pq) (by aesop_cat)

variable {C c r‚ÇÄ}

/-- This means that the differential to an object E_r^{p,q} is zero (both r and (p,q) fixed) -/
class HasEdgeMonoAt (pq : Œπ) (r : ‚Ñ§) [E.HasPage r] : Prop where
  zero : ‚àÄ (pq' : Œπ), (E.page r).d pq' pq = 0

@[simp]
lemma d_eq_zero_of_hasEdgeMonoAt (pq' pq : Œπ) (r : ‚Ñ§) [E.HasPage r] [E.HasEdgeMonoAt pq r] :
    (E.page r).d pq' pq = 0 := by
  apply HasEdgeMonoAt.zero

noncomputable def edgeMonoStep (pq : Œπ) (r r' : ‚Ñ§) (hr : r + 1 = r')
    [E.HasPage r] [E.HasPage r'] [E.HasEdgeMonoAt pq r] :
  (E.page r').X pq ‚ü∂ (E.page r).X pq :=
    (E.iso r r' hr pq).inv ‚â´ ((E.page r).isoHomologyœÄ _ pq rfl (by simp)).inv ‚â´
      (E.page r).iCycles pq

instance (pq : Œπ) (r r' : ‚Ñ§) (hr : r + 1 = r')
    [E.HasPage r] [E.HasPage r'] [E.HasEdgeMonoAt pq r] :
    Mono (E.edgeMonoStep pq r r' hr) := by
  dsimp [edgeMonoStep]
  infer_instance

@[reassoc (attr := simp)]
lemma edgeMonoStep_comp_d (pq pq' : Œπ) (r r' : ‚Ñ§) (hr : r + 1 = r')
    [E.HasPage r] [E.HasPage r'] [E.HasEdgeMonoAt pq r] :
    E.edgeMonoStep pq r r' hr ‚â´ (E.page r).d pq pq' = 0 := by
  simp [edgeMonoStep]

@[reassoc (attr := simp)]
lemma iso_hom_comp_edgeMonoStep (pq : Œπ) (r r' : ‚Ñ§) (hr : r + 1 = r')
    [E.HasPage r] [E.HasPage r'] [E.HasEdgeMonoAt pq r] :
    (E.iso r r' hr pq).hom ‚â´ E.edgeMonoStep pq r r' hr =
      ((E.page r).isoHomologyœÄ _ pq rfl (by simp)).inv ‚â´ (E.page r).iCycles pq := by
  simp [edgeMonoStep]

@[simps]
noncomputable def edgeMonoStepShortComplex (pq pq' : Œπ) (r r' : ‚Ñ§) (hr : r + 1 = r')
    [E.HasPage r] [E.HasPage r'] [E.HasEdgeMonoAt pq r] : ShortComplex C :=
  ShortComplex.mk _ _ (E.edgeMonoStep_comp_d pq pq' r r' hr)

lemma edgeMonoStepShortComplex_exact (pq pq' : Œπ) (r r' : ‚Ñ§) (hr : r + 1 = r')
    (hpq' : (c r).next pq = pq')
    [E.HasPage r] [E.HasPage r'] [E.HasEdgeMonoAt pq r] :
    (E.edgeMonoStepShortComplex pq pq' r r' hr).Exact := by
  subst hpq'
  apply ShortComplex.exact_of_f_is_kernel
  refine IsLimit.ofIsoLimit (((E.page r).sc pq).cyclesIsKernel) ?_
  exact (Fork.ext (((E.page r).isoHomologyœÄ _ pq rfl (by simp)) ‚â™‚â´ E.iso r r' hr pq)
    (by simp; rfl))

/-- This means that the differential from an object E_r^{p,q} is zero (both r and (p,q) fixed) -/
class HasEdgeEpiAt (pq : Œπ) (r : ‚Ñ§) [E.HasPage r] : Prop where
  zero : ‚àÄ (pq' : Œπ), (E.page r).d pq pq' = 0

@[simp]
lemma d_eq_zero_of_hasEdgeEpiAt (pq pq' : Œπ) (r : ‚Ñ§) [E.HasPage r] [E.HasEdgeEpiAt pq r] :
    (E.page r).d pq pq' = 0 := by
  apply HasEdgeEpiAt.zero

noncomputable def edgeEpiStep (pq : Œπ) (r r' : ‚Ñ§) (hr : r + 1 = r')
    [E.HasPage r] [E.HasPage r'] [E.HasEdgeEpiAt pq r] :
  (E.page r).X pq ‚ü∂ (E.page r').X pq :=
    (E.page r).pOpcycles pq ‚â´ ((E.page r).isoHomologyŒπ pq _ rfl (by simp)).inv ‚â´
      (E.iso r r' hr pq).hom

attribute [local instance] epi_comp

instance (pq : Œπ) (r r' : ‚Ñ§) (hr : r + 1 = r')
    [E.HasPage r] [E.HasPage r'] [E.HasEdgeEpiAt pq r] :
    Epi (E.edgeEpiStep pq r r' hr) := by
  dsimp [edgeEpiStep]
  infer_instance

@[reassoc (attr := simp)]
lemma d_comp_edgeEpiStep (pq pq' : Œπ) (r r' : ‚Ñ§) (hr : r + 1 = r')
    [E.HasPage r] [E.HasPage r'] [E.HasEdgeEpiAt pq' r] :
    (E.page r).d pq pq' ‚â´ E.edgeEpiStep pq' r r' hr = 0 := by
  simp [edgeEpiStep]

@[reassoc (attr := simp)]
lemma edgeEpiStep_comp_iso_inv (pq : Œπ) (r r' : ‚Ñ§) (hr : r + 1 = r')
    [E.HasPage r] [E.HasPage r'] [E.HasEdgeEpiAt pq r] :
    E.edgeEpiStep pq r r' hr ‚â´ (E.iso r r' hr pq).inv =
      (E.page r).pOpcycles pq ‚â´ ((E.page r).isoHomologyŒπ pq _ rfl (by simp)).inv := by
  simp [edgeEpiStep]

@[simps]
noncomputable def edgeEpiStepShortComplex (pq pq' : Œπ) (r r' : ‚Ñ§) (hr : r + 1 = r')
    [E.HasPage r] [E.HasPage r'] [E.HasEdgeEpiAt pq' r] : ShortComplex C :=
  ShortComplex.mk _ _ (E.d_comp_edgeEpiStep pq pq' r r' hr)

lemma edgeEpiStepShortComplex_exact (pq pq' : Œπ) (r r' : ‚Ñ§) (hr : r + 1 = r')
    (hpq' : (c r).prev pq' = pq)
    [E.HasPage r] [E.HasPage r'] [E.HasEdgeEpiAt pq' r] :
    (E.edgeEpiStepShortComplex pq pq' r r' hr).Exact := by
  subst hpq'
  apply ShortComplex.exact_of_g_is_cokernel
  refine IsColimit.ofIsoColimit (((E.page r).sc pq').opcyclesIsCokernel) (Iso.symm ?_)
  exact Cofork.ext ((E.iso r r' hr pq').symm ‚â™‚â´ (E.page r).isoHomologyŒπ pq' _ rfl (by simp))
    (by simp; rfl)

@[reassoc (attr := simp)]
lemma edgeMonoStep_edgeEpiStep (pq : Œπ) (r r' : ‚Ñ§) (hr : r + 1 = r') [E.HasPage r] [E.HasPage r']
    [E.HasEdgeMonoAt pq r] [E.HasEdgeEpiAt pq r] :
        E.edgeMonoStep pq r r' hr ‚â´ E.edgeEpiStep pq r r' hr = ùüô _ := by
  simp only [edgeMonoStep, edgeEpiStep, assoc, ‚Üê (E.page r).homology_œÄ_Œπ_assoc pq,
    HomologicalComplex.isoHomologyŒπ_hom_inv_id_assoc,
    HomologicalComplex.isoHomologyœÄ_inv_hom_id_assoc, Iso.inv_hom_id]

@[reassoc (attr := simp)]
lemma edgeEpiStep_edgeMonoStep (pq : Œπ) (r r' : ‚Ñ§) (hr : r + 1 = r') [E.HasPage r] [E.HasPage r']
    [E.HasEdgeMonoAt pq r] [E.HasEdgeEpiAt pq r] :
        E.edgeEpiStep pq r r' hr ‚â´ E.edgeMonoStep pq r r' hr = ùüô _ := by
  simp only [edgeEpiStep, edgeMonoStep,
    ‚Üê cancel_mono ((E.page r).isoHomologyœÄ _ pq rfl (by simp)).hom,
    ‚Üê cancel_mono ((E.page r).isoHomologyŒπ pq _ rfl (by simp)).hom,
    ‚Üê cancel_mono ((E.page r).iCyclesIso pq _ rfl (by simp)).inv,
    HomologicalComplex.iCyclesIso_hom_inv_id, comp_id, id_comp,
    HomologicalComplex.isoHomologyœÄ_hom, HomologicalComplex.isoHomologyœÄ_inv_hom_id,
    HomologicalComplex.isoHomologyŒπ_hom, HomologicalComplex.isoHomologyŒπ_inv_hom_id,
    HomologicalComplex.homology_œÄ_Œπ, HomologicalComplex.iCyclesIso_inv_hom_id_assoc,
    assoc, Iso.hom_inv_id_assoc]

section

variable (r r' : ‚Ñ§) (hr : r + 1 = r') [E.HasPage r] [E.HasPage r']
  (pq pq' pq'' : Œπ) (hpq : (c r).prev pq' = pq) (hpq'' : (c r).next pq' = pq'')
  (left : ((E.page r).sc' pq pq' pq'').LeftHomologyData)
  (right : ((E.page r).sc' pq pq' pq'').RightHomologyData)

@[reassoc (attr := simp)]
lemma leftHomologyData_œÄ_edgeMonoStep_compatibility [E.HasEdgeMonoAt pq' r] :
    left.œÄ ‚â´ left.homologyIso.inv ‚â´ ((E.page r).homologyIsoSc' _ _ _ hpq hpq'').inv ‚â´
      (E.iso r r' hr pq').hom ‚â´ E.edgeMonoStep pq' r r' hr = left.i := by
  simp [edgeMonoStep]

@[reassoc (attr := simp)]
lemma rightHomologyData_p_edgeMonoStep_compatibility [E.HasEdgeMonoAt pq' r] :
      E.edgeMonoStep pq' r r' hr ‚â´ right.p =
       (E.iso r r' hr pq').inv ‚â´ ((E.page r).homologyIsoSc' _ _ _ hpq hpq'').hom ‚â´
       right.homologyIso.hom ‚â´ right.Œπ := by
  rw [‚Üê cancel_epi (E.iso r r' hr pq').hom,
    ‚Üê cancel_epi ((E.page r).isoHomologyœÄ  _ pq' rfl (by apply d_eq_zero_of_hasEdgeMonoAt)).hom]
  simp [edgeMonoStep]

@[reassoc (attr := simp)]
lemma leftHomologyData_i_edgeEpiStep_compatibility [E.HasEdgeEpiAt pq' r] :
    left.i ‚â´ E.edgeEpiStep pq' r r' hr =
      left.œÄ ‚â´ left.homologyIso.inv ‚â´ ((E.page r).homologyIsoSc' _ _ _ hpq hpq'').inv ‚â´
        (E.iso r r' hr pq').hom := by
  rw [‚Üê cancel_mono (E.iso r r' hr pq').inv,
    ‚Üê cancel_mono ((E.page r).isoHomologyŒπ  pq' pq'' hpq'' (by apply d_eq_zero_of_hasEdgeEpiAt)).hom]
  simp [edgeEpiStep]

@[reassoc (attr := simp)]
lemma rightHomologyData_Œπ_edgeEpiStep_compatibility [E.HasEdgeEpiAt pq' r] :
    E.edgeEpiStep pq' r r' hr ‚â´ (E.iso r r' hr pq').inv ‚â´
      ((E.page r).homologyIsoSc' _ _ _ hpq hpq'').hom ‚â´
        right.homologyIso.hom ‚â´ right.Œπ = right.p := by
  simp [edgeEpiStep]

end


def hasEdgeMonoSet (pq : Œπ) : Set ‚Ñ§  :=
  fun r => ‚àÄ (r' : ‚Ñ§) (_ : r ‚â§ r'), ‚àÉ (_ : E.HasPage r'), E.HasEdgeMonoAt pq r'

def hasEdgeEpiSet (pq : Œπ) : Set ‚Ñ§ :=
  fun r => ‚àÄ (r' : ‚Ñ§) (_ : r ‚â§ r'), ‚àÉ (_ : E.HasPage r'), E.HasEdgeEpiAt pq r'

section

lemma isIso_hom_succ (f : E ‚ü∂ E') (r : ‚Ñ§) [E.HasPage r] [E'.HasPage r]
    (hf : IsIso (f.hom r)) : IsIso (f.hom (r + 1)) := by
  have : ‚àÄ (pq : Œπ), IsIso ((f.hom (r + 1)).f pq) := fun pq => by
    have : IsIso (HomologicalComplex.homologyMap (Hom.hom f r) pq) := by
      -- this should already be an instance
      change IsIso ((HomologicalComplex.homologyFunctor _ _ pq).map (Hom.hom f r))
      infer_instance
    exact IsIso.of_isIso_fac_left (Hom.comm f r _ rfl pq).symm
  apply HomologicalComplex.Hom.isIso_of_components

lemma isIso_hom_of_GE (f : E ‚ü∂ E') (r r' : ‚Ñ§) (hrr' : r ‚â§ r')
    [E.HasPage r] [E'.HasPage r] [E.HasPage r'] [E'.HasPage r']
    (hf : IsIso (f.hom r)) : IsIso (f.hom r') := by
  obtain ‚ü®k, hk‚ü© := Int.eq_add_ofNat_of_le hrr'
  revert r' hrr'
  induction' k with k hk
  ¬∑ intro r' _ _ _ hr'
    obtain rfl : r = r' := by simp [hr']
    exact hf
  ¬∑ intro r' hrr' _ _ hr'
    obtain rfl : r' = (r + k) + 1 := by simp only [hr', Nat.cast_succ, add_assoc]
    exact isIso_hom_succ f (r + k) (hk (r + k) (by linarith) (by rfl))

end

end SpectralSequence

abbrev CohomologicalSpectralSequence :=
  SpectralSequence C (fun r => ComplexShape.up' (‚ü®r, 1 - r‚ü© : ‚Ñ§ √ó ‚Ñ§))

abbrev E‚ÇÇCohomologicalSpectralSequence := CohomologicalSpectralSequence C 2

abbrev CohomologicalSpectralSequenceNat :=
  SpectralSequence C (fun r => ComplexShape.spectralSequenceNat ‚ü®r, 1 - r‚ü©)

abbrev E‚ÇÇCohomologicalSpectralSequenceNat :=
  CohomologicalSpectralSequenceNat C 2

abbrev CohomologicalSpectralSequenceFin (l : ‚Ñï) :=
  SpectralSequence C (fun r => ComplexShape.spectralSequenceFin l ‚ü®r, 1 - r‚ü©)

abbrev E‚ÇÇCohomologicalSpectralSequenceFin (l : ‚Ñï) :=
  CohomologicalSpectralSequenceFin C 2 l

end CategoryTheory
