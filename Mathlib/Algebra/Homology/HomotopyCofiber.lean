/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.HomologicalComplexBiprod
import Mathlib.Algebra.Homology.Homotopy
import Mathlib.CategoryTheory.MorphismProperty.IsInvertedBy

/-! The homotopy cofiber of a morphism of homological complexes

In this file, we construct the homotopy cofiber of a morphism `Ï† : F âŸ¶ G`
between homological complexes in `HomologicalComplex C c`. In degree `i`,
it is isomorphic to `(F.X j) âŠ (G.X i)` if there is a `j` such that `c.Rel i j`,
and `G.X i` otherwise. (This is also known as the mapping cone of `Ï†`. Under
the name `CochainComplex.mappingCone`, a specific API shall be developed
for the case of cochain complexes indexed by `â„¤`.)

When we assume `hc : âˆ€ j, âˆƒ i, c.Rel i j` (which holds in the case of chain complexes,
or cochain complexes indexed by `â„¤`), then for any homological complex `K`,
there is a bijection `HomologicalComplex.homotopyCofiber.descEquiv Ï† K hc`
between `homotopyCofiber Ï† âŸ¶ K` and the tuples `(Î±, hÎ±)` with
`Î± : G âŸ¶ K` and `hÎ± : Homotopy (Ï† â‰« Î±) 0`.

We shall also study the cylinder of a homological complex `K`: this is the
homotopy cofiber of the morphism  `biprod.lift (ğŸ™ K) (-ğŸ™ K) : K âŸ¶ K âŠ K`.
Then, a morphism `K.cylinder âŸ¶ M` is determined by the data of two
morphisms `Ï†â‚€ Ï†â‚ : K âŸ¶ M` and a homotopy `h : Homotopy Ï†â‚€ Ï†â‚`,
see `cylinder.desc`. There is also a homotopy equivalence
`cylinder.homotopyEquiv K : HomotopyEquiv K.cylinder K`. From the construction of
the cylinder, we deduce the lemma `Homotopy.map_eq_of_inverts_homotopyEquivalences`
which assert that if a functor inverts homotopy equivalences, then the image of
two homotopic maps are equal.

-/


open CategoryTheory Category Limits Preadditive

variable {C : Type*} [Category C] [Preadditive C]

namespace HomologicalComplex

variable {Î¹ : Type*} {c : ComplexShape Î¹} {F G K : HomologicalComplex C c} (Ï† : F âŸ¶ G)

/-- A morphism of homological complexes `Ï† : F âŸ¶ G` has a homotopy cofiber if for all
indices `i` and `j` such that `c.Rel i j`, the binary biproduct `F.X j âŠ G.X i` exists. -/
class HasHomotopyCofiber (Ï† : F âŸ¶ G) : Prop where
  hasBinaryBiproduct (i j : Î¹) (hij : c.Rel i j) : HasBinaryBiproduct (F.X j) (G.X i)

instance [HasBinaryBiproducts C] : HasHomotopyCofiber Ï† where
  hasBinaryBiproduct _ _ _ := inferInstance

variable [HasHomotopyCofiber Ï†] [DecidableRel c.Rel]

namespace homotopyCofiber

/-- The `X` field of the homological complex `homotopyCofiber Ï†`. -/
noncomputable def X (i : Î¹) : C :=
  if hi : c.Rel i (c.next i)
  then
    haveI := HasHomotopyCofiber.hasBinaryBiproduct Ï† _ _ hi
    (F.X (c.next i)) âŠ (G.X i)
  else G.X i

/-- The canonical isomorphism `(homotopyCofiber Ï†).X i â‰… F.X j âŠ G.X i` when `c.Rel i j`. -/
noncomputable def XIsoBiprod (i j : Î¹) (hij : c.Rel i j) [HasBinaryBiproduct (F.X j) (G.X i)] :
    X Ï† i â‰… F.X j âŠ G.X i :=
  eqToIso (by
    obtain rfl := c.next_eq' hij
    apply dif_pos hij)

/-- The canonical isomorphism `(homotopyCofiber Ï†).X i â‰… G.X i` when `Â¬ c.Rel i (c.next i)`. -/
noncomputable def XIso (i : Î¹) (hi : Â¬ c.Rel i (c.next i)) :
    X Ï† i â‰… G.X i :=
  eqToIso (dif_neg hi)

/-- The second projection `(homotopyCofiber Ï†).X i âŸ¶ G.X i`. -/
noncomputable def sndX (i : Î¹) : X Ï† i âŸ¶ G.X i :=
  if hi : c.Rel i (c.next i)
  then
    haveI := HasHomotopyCofiber.hasBinaryBiproduct Ï† _ _ hi
    (XIsoBiprod Ï† _ _ hi).hom â‰« biprod.snd
  else
    (XIso Ï† i hi).hom

/-- The right inclusion `G.X i âŸ¶ (homotopyCofiber Ï†).X i`. -/
noncomputable def inrX (i : Î¹) : G.X i âŸ¶ X Ï† i :=
  if hi : c.Rel i (c.next i)
  then
    haveI := HasHomotopyCofiber.hasBinaryBiproduct Ï† _ _ hi
    biprod.inr â‰« (XIsoBiprod Ï† _ _ hi).inv
  else
    (XIso Ï† i hi).inv

@[reassoc (attr := simp)]
lemma inrX_sndX (i : Î¹) : inrX Ï† i â‰« sndX Ï† i = ğŸ™ _ := by
  dsimp [sndX, inrX]
  split_ifs with hi <;> simp

@[reassoc]
lemma sndX_inrX (i : Î¹) (hi : Â¬ c.Rel i (c.next i)) :
    sndX Ï† i â‰« inrX Ï† i = ğŸ™ _ := by
  dsimp [sndX, inrX]
  simp only [dif_neg hi, Iso.hom_inv_id]

/-- The first projection `(homotopyCofiber Ï†).X i âŸ¶ F.X j` when `c.Rel i j`. -/
noncomputable def fstX (i j : Î¹) (hij : c.Rel i j) : X Ï† i âŸ¶ F.X j :=
  haveI := HasHomotopyCofiber.hasBinaryBiproduct Ï† _ _ hij
  (XIsoBiprod Ï† i j hij).hom â‰« biprod.fst

/-- The left inclusion `F.X i âŸ¶ (homotopyCofiber Ï†).X j` when `c.Rel j i`. -/
noncomputable def inlX (i j : Î¹) (hij : c.Rel j i) : F.X i âŸ¶ X Ï† j :=
  haveI := HasHomotopyCofiber.hasBinaryBiproduct Ï† _ _ hij
  biprod.inl â‰« (XIsoBiprod Ï† j i hij).inv

@[reassoc (attr := simp)]
lemma inlX_fstX (i j : Î¹) (hij : c.Rel j i) :
    inlX Ï† i j hij â‰« fstX Ï† j i hij = ğŸ™ _ := by
  simp [inlX, fstX]

@[reassoc (attr := simp)]
lemma inlX_sndX (i j : Î¹) (hij : c.Rel j i) :
    inlX Ï† i j hij â‰« sndX Ï† j = 0 := by
  obtain rfl := c.next_eq' hij
  simp [inlX, sndX, dif_pos hij]

@[reassoc (attr := simp)]
lemma inrX_fstX (i j : Î¹) (hij : c.Rel i j) :
    inrX Ï† i â‰« fstX Ï† i j hij = 0 := by
  obtain rfl := c.next_eq' hij
  simp [inrX, fstX, dif_pos hij]

/-- The `d` field of the homological complex `homotopyCofiber Ï†`. -/
noncomputable def d (i j : Î¹) : X Ï† i âŸ¶ X Ï† j :=
  if hij : c.Rel i j
  then
    (if hj : c.Rel j (c.next j) then -fstX Ï† i j hij â‰« F.d _ _ â‰« inlX Ï† _ _ hj else 0) +
      fstX Ï† i j hij â‰« Ï†.f j â‰« inrX Ï† j + sndX Ï† i â‰« G.d i j â‰« inrX Ï† j
  else
    0

lemma ext_to_X (i j : Î¹) (hij : c.Rel i j) {A : C} {f g : A âŸ¶ X Ï† i}
    (hâ‚ : f â‰« fstX Ï† i j hij = g â‰« fstX Ï† i j hij) (hâ‚‚ : f â‰« sndX Ï† i = g â‰« sndX Ï† i) :
    f = g := by
  haveI := HasHomotopyCofiber.hasBinaryBiproduct Ï† _ _ hij
  rw [â† cancel_mono (XIsoBiprod Ï† i j hij).hom]
  apply biprod.hom_ext
  Â· simpa using hâ‚
  Â· obtain rfl := c.next_eq' hij
    simpa [sndX, dif_pos hij] using hâ‚‚

lemma ext_to_X' (i : Î¹) (hi : Â¬ c.Rel i (c.next i)) {A : C} {f g : A âŸ¶ X Ï† i}
    (h : f â‰« sndX Ï† i = g â‰« sndX Ï† i) : f = g := by
  rw [â† cancel_mono (XIso Ï† i hi).hom]
  simpa only [sndX, dif_neg hi] using h

lemma ext_from_X (i j : Î¹) (hij : c.Rel j i) {A : C} {f g : X Ï† j âŸ¶ A}
    (hâ‚ : inlX Ï† i j hij â‰« f = inlX Ï† i j hij â‰« g) (hâ‚‚ : inrX Ï† j â‰« f = inrX Ï† j â‰« g) :
    f = g := by
  haveI := HasHomotopyCofiber.hasBinaryBiproduct Ï† _ _ hij
  rw [â† cancel_epi (XIsoBiprod Ï† j i hij).inv]
  apply biprod.hom_ext'
  Â· simpa [inlX] using hâ‚
  Â· obtain rfl := c.next_eq' hij
    simpa [inrX, dif_pos hij] using hâ‚‚

lemma ext_from_X' (i : Î¹) (hi : Â¬ c.Rel i (c.next i)) {A : C} {f g : X Ï† i âŸ¶ A}
    (h : inrX Ï† i â‰« f = inrX Ï† i â‰« g) : f = g := by
  rw [â† cancel_epi (XIso Ï† i hi).inv]
  simpa only [inrX, dif_neg hi] using h

@[reassoc]
lemma d_fstX (i j k : Î¹) (hij : c.Rel i j) (hjk : c.Rel j k) :
    d Ï† i j â‰« fstX Ï† j k hjk = -fstX Ï† i j hij â‰« F.d j k := by
  obtain rfl := c.next_eq' hjk
  simp [d, dif_pos hij, dif_pos hjk]

@[reassoc]
lemma d_sndX (i j : Î¹) (hij : c.Rel i j) :
    d Ï† i j â‰« sndX Ï† j = fstX Ï† i j hij â‰« Ï†.f j + sndX Ï† i â‰« G.d i j := by
  dsimp [d]
  split_ifs with hij <;> simp

@[reassoc]
lemma inlX_d (i j k : Î¹) (hij : c.Rel i j) (hjk : c.Rel j k) :
    inlX Ï† j i hij â‰« d Ï† i j = -F.d j k â‰« inlX Ï† k j hjk + Ï†.f j â‰« inrX Ï† j := by
  apply ext_to_X Ï† j k hjk
  Â· simp [d_fstX Ï†  _ _ _ hij hjk]
  Â· simp [d_sndX Ï† _ _ hij]

@[reassoc]
lemma inlX_d' (i j : Î¹) (hij : c.Rel i j) (hj : Â¬ c.Rel j (c.next j)) :
    inlX Ï† j i hij â‰« d Ï† i j = Ï†.f j â‰« inrX Ï† j := by
  apply ext_to_X' _ _ hj
  simp [d_sndX Ï† i j hij]

lemma shape (i j : Î¹) (hij : Â¬ c.Rel i j) :
    d Ï† i j = 0 :=
  dif_neg hij

@[reassoc (attr := simp)]
lemma inrX_d (i j : Î¹) :
    inrX Ï† i â‰« d Ï† i j = G.d i j â‰« inrX Ï† j := by
  by_cases hij : c.Rel i j
  Â· by_cases hj : c.Rel j (c.next j)
    Â· apply ext_to_X _ _ _ hj
      Â· simp [d_fstX Ï† _ _ _ hij]
      Â· simp [d_sndX Ï† _ _ hij]
    Â· apply ext_to_X' _ _ hj
      simp [d_sndX Ï† _ _ hij]
  Â· rw [shape Ï† _ _ hij, G.shape _ _ hij, zero_comp, comp_zero]

end homotopyCofiber

/-- The homotopy cofiber of a morphism of homological complexes,
also known as the mapping cone. -/
@[simps]
noncomputable def homotopyCofiber : HomologicalComplex C c where
  X i := homotopyCofiber.X Ï† i
  d i j := homotopyCofiber.d Ï† i j
  shape i j hij := homotopyCofiber.shape Ï† i j hij
  d_comp_d' i j k hij hjk := by
    apply homotopyCofiber.ext_from_X Ï† j i hij
    Â· dsimp
      simp only [comp_zero, homotopyCofiber.inlX_d_assoc Ï† i j k hij hjk,
        add_comp, assoc, homotopyCofiber.inrX_d, Hom.comm_assoc, neg_comp]
      by_cases hk : c.Rel k (c.next k)
      Â· simp [homotopyCofiber.inlX_d Ï† j k _ hjk hk]
      Â· simp [homotopyCofiber.inlX_d' Ï† j k hjk hk]
    Â· simp

namespace homotopyCofiber

/-- The right inclusion `G âŸ¶ homotopyCofiber Ï†`. -/
@[simps!]
noncomputable def inr : G âŸ¶ homotopyCofiber Ï† where
  f i := inrX Ï† i

section

/-- The composition `Ï† â‰« mappingCone.inr Ï†` is homotopic to `0`. -/
noncomputable def inrCompHomotopy (hc : âˆ€ j, âˆƒ i, c.Rel i j) :
    Homotopy (Ï† â‰« inr Ï†) 0 where
  hom i j :=
    if hij : c.Rel j i then inlX Ï† i j hij else 0
  zero _ _ hij := dif_neg hij
  comm j := by
    obtain âŸ¨i, hijâŸ© := hc j
    rw [prevD_eq _ hij, dif_pos hij]
    by_cases hj : c.Rel j (c.next j)
    Â· simp only [comp_f, homotopyCofiber_d, zero_f, add_zero,
        inlX_d Ï† i j _ hij hj, dNext_eq _ hj, dif_pos hj,
        add_neg_cancel_left, inr_f]
    Â· rw [dNext_eq_zero _ _  hj, zero_add, zero_f, add_zero, homotopyCofiber_d,
        inlX_d' _ _ _ _ hj, comp_f, inr_f]

variable (hc : âˆ€ j, âˆƒ i, c.Rel i j)

lemma inrCompHomotopy_hom (i j : Î¹) (hij : c.Rel j i) :
    (inrCompHomotopy Ï† hc).hom i j = inlX Ï† i j hij := dif_pos hij

lemma inrCompHomotopy_hom_eq_zero (i j : Î¹) (hij : Â¬ c.Rel j i) :
    (inrCompHomotopy Ï† hc).hom i j = 0 := dif_neg hij

end

section

variable (Î± : G âŸ¶ K) (hÎ± : Homotopy (Ï† â‰« Î±) 0)

/-- The morphism `homotopyCofiber Ï† âŸ¶ K` that is induced by a morphism `Î± : G âŸ¶ K`
and a homotopy `hÎ± : Homotopy (Ï† â‰« Î±) 0`. -/
noncomputable def desc :
    homotopyCofiber Ï† âŸ¶ K where
  f j :=
    if hj : c.Rel j (c.next j)
    then fstX Ï† j _ hj â‰« hÎ±.hom _ j + sndX Ï† j â‰« Î±.f j
    else sndX Ï† j â‰« Î±.f j
  comm' j k hjk := by
    obtain rfl := c.next_eq' hjk
    simp [dif_pos hjk]
    have H := hÎ±.comm (c.next j)
    simp only [comp_f, zero_f, add_zero, prevD_eq _ hjk] at H
    split_ifs with hj
    Â· simp only [comp_add, d_sndX_assoc _ _ _ hjk, add_comp, assoc, H,
        d_fstX_assoc _ _ _ _ hjk, neg_comp, dNext, AddMonoidHom.mk'_apply]
      abel
    Â· simp only [d_sndX_assoc _ _ _ hjk, add_comp, assoc, H, dNext_eq_zero _ _ hj, zero_add]

lemma desc_f (j k : Î¹) (hjk : c.Rel j k) :
    (desc Ï† Î± hÎ±).f j = fstX Ï† j _ hjk â‰« hÎ±.hom _ j + sndX Ï† j â‰« Î±.f j := by
  obtain rfl := c.next_eq' hjk
  apply dif_pos hjk

lemma desc_f' (j : Î¹) (hj : Â¬ c.Rel j (c.next j)) :
    (desc Ï† Î± hÎ±).f j = sndX Ï† j â‰« Î±.f j := by
  apply dif_neg hj

@[reassoc (attr := simp)]
lemma inlX_desc_f (i j : Î¹) (hjk : c.Rel j i) :
    inlX Ï† i j hjk â‰« (desc Ï† Î± hÎ±).f j = hÎ±.hom i j := by
  obtain rfl := c.next_eq' hjk
  dsimp [desc]
  rw [dif_pos hjk, comp_add, inlX_fstX_assoc, inlX_sndX_assoc, zero_comp, add_zero]

@[reassoc (attr := simp)]
lemma inrX_desc_f (i : Î¹) :
    inrX Ï† i â‰« (desc Ï† Î± hÎ±).f i = Î±.f i := by
  dsimp [desc]
  split_ifs <;> simp

@[reassoc (attr := simp)]
lemma inr_desc :
    inr Ï† â‰« desc Ï† Î± hÎ± = Î± := by cat_disch

@[reassoc (attr := simp)]
lemma inrCompHomotopy_hom_desc_hom (hc : âˆ€ j, âˆƒ i, c.Rel i j) (i j : Î¹) :
    (inrCompHomotopy Ï† hc).hom i j â‰« (desc Ï† Î± hÎ±).f j = hÎ±.hom i j := by
  by_cases hij : c.Rel j i
  Â· dsimp
    simp only [inrCompHomotopy_hom Ï† hc i j hij, desc_f Ï† Î± hÎ± _ _ hij,
      comp_add, inlX_fstX_assoc, inlX_sndX_assoc, zero_comp, add_zero]
  Â· simp only [Homotopy.zero _ _ _ hij, zero_comp]

lemma eq_desc (f : homotopyCofiber Ï† âŸ¶ K) (hc : âˆ€ j, âˆƒ i, c.Rel i j) :
    f = desc Ï† (inr Ï† â‰« f) (Homotopy.trans (Homotopy.ofEq (by simp))
      (((inrCompHomotopy Ï† hc).compRight f).trans (Homotopy.ofEq (by simp)))) := by
  ext j
  by_cases hj : c.Rel j (c.next j)
  Â· apply ext_from_X Ï† _ _ hj
    Â· simp [inrCompHomotopy_hom _ _ _ _ hj]
    Â· simp
  Â· apply ext_from_X' Ï† _ hj
    simp

end

lemma descSigma_ext_iff {Ï† : F âŸ¶ G} {K : HomologicalComplex C c}
    (x y : Î£ (Î± : G âŸ¶ K), Homotopy (Ï† â‰« Î±) 0) :
    x = y â†” x.1 = y.1 âˆ§ (âˆ€ (i j : Î¹) (_ : c.Rel j i), x.2.hom i j = y.2.hom i j) := by
  constructor
  Â· rintro rfl
    tauto
  Â· obtain âŸ¨xâ‚, xâ‚‚âŸ© := x
    obtain âŸ¨yâ‚, yâ‚‚âŸ© := y
    rintro âŸ¨rfl, hâŸ©
    simp only [Sigma.mk.inj_iff, heq_eq_eq, true_and]
    ext i j
    by_cases hij : c.Rel j i
    Â· exact h _ _ hij
    Â· simp only [Homotopy.zero _ _ _ hij]

/-- Morphisms `homotopyCofiber Ï† âŸ¶ K` are uniquely determined by
a morphism `Î± : G âŸ¶ K` and a homotopy from `Ï† â‰« Î±` to `0`. -/
noncomputable def descEquiv (K : HomologicalComplex C c) (hc : âˆ€ j, âˆƒ i, c.Rel i j) :
    (Î£ (Î± : G âŸ¶ K), Homotopy (Ï† â‰« Î±) 0) â‰ƒ (homotopyCofiber Ï† âŸ¶ K) where
  toFun := fun âŸ¨Î±, hÎ±âŸ© => desc Ï† Î± hÎ±
  invFun f := âŸ¨inr Ï† â‰« f, Homotopy.trans (Homotopy.ofEq (by simp))
    (((inrCompHomotopy Ï† hc).compRight f).trans (Homotopy.ofEq (by simp)))âŸ©
  right_inv f := (eq_desc Ï† f hc).symm
  left_inv := fun âŸ¨Î±, hÎ±âŸ© => by
    rw [descSigma_ext_iff]
    cat_disch

end homotopyCofiber

section

variable (K)
variable [âˆ€ i, HasBinaryBiproduct (K.X i) (K.X i)]
  [HasHomotopyCofiber (biprod.lift (ğŸ™ K) (-ğŸ™ K))]

/-- The cylinder object of a homological complex `K` is the homotopy cofiber
of the morphism  `biprod.lift (ğŸ™ K) (-ğŸ™ K) : K âŸ¶ K âŠ K`. -/
noncomputable abbrev cylinder := homotopyCofiber (biprod.lift (ğŸ™ K) (-ğŸ™ K))

namespace cylinder

/-- The left inclusion `K âŸ¶ K.cylinder`. -/
noncomputable def Î¹â‚€ : K âŸ¶ K.cylinder := biprod.inl â‰« homotopyCofiber.inr _

/-- The right inclusion `K âŸ¶ K.cylinder`. -/
noncomputable def Î¹â‚ : K âŸ¶ K.cylinder := biprod.inr â‰« homotopyCofiber.inr _

variable {K}

section

variable (Ï†â‚€ Ï†â‚ : K âŸ¶ F) (h : Homotopy Ï†â‚€ Ï†â‚)

/-- The morphism `K.cylinder âŸ¶ F` that is induced by two morphisms `Ï†â‚€ Ï†â‚ : K âŸ¶ F`
and a homotopy `h : Homotopy Ï†â‚€ Ï†â‚`. -/
noncomputable def desc : K.cylinder âŸ¶ F :=
  homotopyCofiber.desc _ (biprod.desc Ï†â‚€ Ï†â‚)
    (Homotopy.trans (Homotopy.ofEq (by
      simp only [biprod.lift_desc, id_comp, neg_comp, sub_eq_add_neg]))
      ((Homotopy.equivSubZero h)))

@[reassoc (attr := simp)]
lemma Î¹â‚€_desc : Î¹â‚€ K â‰« desc Ï†â‚€ Ï†â‚ h = Ï†â‚€ := by simp [Î¹â‚€, desc]

@[reassoc (attr := simp)]
lemma Î¹â‚_desc : Î¹â‚ K â‰« desc Ï†â‚€ Ï†â‚ h = Ï†â‚ := by simp [Î¹â‚, desc]

end

variable (K)

/-- The projection `Ï€ : K.cylinder âŸ¶ K`. -/
noncomputable def Ï€ : K.cylinder âŸ¶ K := desc (ğŸ™ K) (ğŸ™ K) (Homotopy.refl _)

@[reassoc (attr := simp)]
lemma Î¹â‚€_Ï€ : Î¹â‚€ K â‰« Ï€ K = ğŸ™ K := by simp [Ï€]

@[reassoc (attr := simp)]
lemma Î¹â‚_Ï€ : Î¹â‚ K â‰« Ï€ K = ğŸ™ K := by simp [Ï€]

/-- The left inclusion `K.X i âŸ¶ K.cylinder.X j` when `c.Rel j i`. -/
noncomputable abbrev inlX (i j : Î¹) (hij : c.Rel j i) : K.X i âŸ¶ K.cylinder.X j :=
  homotopyCofiber.inlX (biprod.lift (ğŸ™ K) (-ğŸ™ K)) i j hij

/-- The right inclusion `(K âŠ K).X i âŸ¶ K.cylinder.X i`. -/
noncomputable abbrev inrX (i : Î¹) : (K âŠ K).X i âŸ¶ K.cylinder.X i :=
  homotopyCofiber.inrX (biprod.lift (ğŸ™ K) (-ğŸ™ K)) i

@[reassoc (attr := simp)]
lemma inlX_Ï€ (i j : Î¹) (hij : c.Rel j i) :
    inlX K i j hij â‰« (Ï€ K).f j = 0 := by
  erw [homotopyCofiber.inlX_desc_f]
  simp [Homotopy.equivSubZero]

@[reassoc (attr := simp)]
lemma inrX_Ï€ (i : Î¹) :
    inrX K i â‰« (Ï€ K).f i = (biprod.desc (ğŸ™ _) (ğŸ™ K)).f i :=
  homotopyCofiber.inrX_desc_f _ _ _ _

section

variable (hc : âˆ€ j, âˆƒ i, c.Rel i j)

namespace Ï€CompÎ¹â‚€Homotopy

/-- A null homotopic map `K.cylinder âŸ¶ K.cylinder` which identifies to
`Ï€ K â‰« Î¹â‚€ K - ğŸ™ _`, see `nullHomotopicMap_eq`. -/
noncomputable def nullHomotopicMap : K.cylinder âŸ¶ K.cylinder :=
  Homotopy.nullHomotopicMap'
    (fun i j hij => homotopyCofiber.sndX (biprod.lift (ğŸ™ K) (-ğŸ™ K)) i â‰«
      (biprod.snd : K âŠ K âŸ¶ K).f i â‰« inlX K i j hij)

/-- The obvious homotopy from `nullHomotopicMap K` to zero. -/
noncomputable def nullHomotopy : Homotopy (nullHomotopicMap K) 0 :=
  Homotopy.nullHomotopy' _

lemma inlX_nullHomotopy_f (i j : Î¹) (hij : c.Rel j i) :
    inlX K i j hij â‰« (nullHomotopicMap K).f j =
      inlX K i j hij â‰« (Ï€ K â‰« Î¹â‚€ K - ğŸ™ _).f j := by
  dsimp [nullHomotopicMap]
  by_cases hj : âˆƒ (k : Î¹), c.Rel k j
  Â· obtain âŸ¨k, hjkâŸ© := hj
    simp only [assoc, Homotopy.nullHomotopicMap'_f hjk hij, homotopyCofiber_X, homotopyCofiber_d,
      homotopyCofiber.d_sndX_assoc _ _ _ hij, add_comp, comp_add, homotopyCofiber.inlX_fstX_assoc,
      homotopyCofiber.inlX_sndX_assoc, zero_comp, add_zero, comp_sub, inlX_Ï€_assoc, comp_id,
      zero_sub, â† HomologicalComplex.comp_f_assoc, biprod.lift_snd, neg_f_apply, id_f,
      neg_comp, id_comp]
  Â· simp only [not_exists] at hj
    simp only [Homotopy.nullHomotopicMap'_f_of_not_rel_right hij hj,
      homotopyCofiber_X, homotopyCofiber_d, assoc, comp_sub, comp_id,
      homotopyCofiber.d_sndX_assoc _ _ _ hij, add_comp, comp_add, zero_comp, add_zero,
      homotopyCofiber.inlX_fstX_assoc, homotopyCofiber.inlX_sndX_assoc,
      â† HomologicalComplex.comp_f_assoc, biprod.lift_snd, neg_f_apply, id_f, neg_comp,
      id_comp, inlX_Ï€_assoc, zero_sub]

include hc

lemma inrX_nullHomotopy_f (j : Î¹) :
    inrX K j â‰« (nullHomotopicMap K).f j = inrX K j â‰« (Ï€ K â‰« Î¹â‚€ K - ğŸ™ _).f j := by
  have : biprod.lift (ğŸ™ K) (-ğŸ™ K) = biprod.inl - biprod.inr :=
    biprod.hom_ext _ _ (by simp) (by simp)
  obtain âŸ¨i, hijâŸ© := hc j
  dsimp [nullHomotopicMap]
  by_cases hj : âˆƒ (k : Î¹), c.Rel j k
  Â· obtain âŸ¨k, hjkâŸ© := hj
    simp only [Homotopy.nullHomotopicMap'_f hij hjk,
      homotopyCofiber_X, homotopyCofiber_d, assoc, comp_add,
      homotopyCofiber.inrX_d_assoc, homotopyCofiber.inrX_sndX_assoc, comp_sub,
      inrX_Ï€_assoc, comp_id, â† Hom.comm_assoc, homotopyCofiber.inlX_d _ _ _ _ _ hjk,
      comp_neg, add_neg_cancel_left]
    rw [â† cancel_epi (biprodXIso K K j).inv]
    ext
    Â· simp [Î¹â‚€]
    Â· dsimp
      simp only [inr_biprodXIso_inv_assoc, biprod_inr_snd_f_assoc, comp_sub,
        biprod_inr_desc_f_assoc, id_f, id_comp, Î¹â‚€, comp_f, this,
        sub_f_apply, sub_comp, homotopyCofiber_X, homotopyCofiber.inr_f]
  Â· simp only [not_exists] at hj
    simp only [assoc, Homotopy.nullHomotopicMap'_f_of_not_rel_left hij hj, homotopyCofiber_X,
      homotopyCofiber_d, homotopyCofiber.inlX_d' _ _ _ _ (hj _), homotopyCofiber.inrX_sndX_assoc,
      comp_sub, inrX_Ï€_assoc, comp_id, Î¹â‚€, comp_f, homotopyCofiber.inr_f]
    rw [â† cancel_epi (biprodXIso K K j).inv]
    ext
    Â· simp
    Â· simp [this]

lemma nullHomotopicMap_eq : nullHomotopicMap K = Ï€ K â‰« Î¹â‚€ K - ğŸ™ _ := by
  ext i
  by_cases hi : c.Rel i (c.next i)
  Â· exact homotopyCofiber.ext_from_X (biprod.lift (ğŸ™ K) (-ğŸ™ K)) (c.next i) i hi
      (inlX_nullHomotopy_f _ _ _ _) (inrX_nullHomotopy_f _ hc _)
  Â· exact homotopyCofiber.ext_from_X' (biprod.lift (ğŸ™ K) (-ğŸ™ K)) _ hi (inrX_nullHomotopy_f _ hc _)

end Ï€CompÎ¹â‚€Homotopy

/-- The homotopy between `Ï€ K â‰« Î¹â‚€ K` and `ğŸ™ K.cylinder`. -/
noncomputable def Ï€CompÎ¹â‚€Homotopy : Homotopy (Ï€ K â‰« Î¹â‚€ K) (ğŸ™ K.cylinder) :=
  Homotopy.equivSubZero.symm
    ((Homotopy.ofEq (Ï€CompÎ¹â‚€Homotopy.nullHomotopicMap_eq K hc).symm).trans
      (Ï€CompÎ¹â‚€Homotopy.nullHomotopy K))

/-- The homotopy equivalence between `K.cylinder` and `K`. -/
noncomputable def homotopyEquiv : HomotopyEquiv K.cylinder K where
  hom := Ï€ K
  inv := Î¹â‚€ K
  homotopyHomInvId := Ï€CompÎ¹â‚€Homotopy K hc
  homotopyInvHomId := Homotopy.ofEq (by simp)

/-- The homotopy between `cylinder.Î¹â‚€ K` and `cylinder.Î¹â‚ K`. -/
noncomputable def homotopyâ‚€â‚ : Homotopy (Î¹â‚€ K) (Î¹â‚ K) :=
  (Homotopy.ofEq (by simp)).trans (((Ï€CompÎ¹â‚€Homotopy K hc).compLeft (Î¹â‚ K)).trans
    (Homotopy.ofEq (by simp)))

include hc in
lemma map_Î¹â‚€_eq_map_Î¹â‚ {D : Type*} [Category D] (H : HomologicalComplex C c â¥¤ D)
    (hH : (homotopyEquivalences C c).IsInvertedBy H) :
    H.map (Î¹â‚€ K) = H.map (Î¹â‚ K) := by
  have : IsIso (H.map (cylinder.Ï€ K)) := hH _ âŸ¨homotopyEquiv K hc, rflâŸ©
  simp only [â† cancel_mono (H.map (cylinder.Ï€ K)), â† H.map_comp, Î¹â‚€_Ï€, H.map_id, Î¹â‚_Ï€]

end

end cylinder

/-- If a functor inverts homotopy equivalences, it sends homotopic maps to the same map. -/
lemma _root_.Homotopy.map_eq_of_inverts_homotopyEquivalences
    {Ï†â‚€ Ï†â‚ : F âŸ¶ G} (h : Homotopy Ï†â‚€ Ï†â‚) (hc : âˆ€ j, âˆƒ i, c.Rel i j)
    [âˆ€ i, HasBinaryBiproduct (F.X i) (F.X i)]
    [HasHomotopyCofiber (biprod.lift (ğŸ™ F) (-ğŸ™ F))]
    {D : Type*} [Category D] (H : HomologicalComplex C c â¥¤ D)
    (hH : (homotopyEquivalences C c).IsInvertedBy H) :
    H.map Ï†â‚€ = H.map Ï†â‚ := by
  simp only [â† cylinder.Î¹â‚€_desc _ _ h, â† cylinder.Î¹â‚_desc _ _ h, H.map_comp,
    cylinder.map_Î¹â‚€_eq_map_Î¹â‚ _ hc _ hH]

end

end HomologicalComplex
