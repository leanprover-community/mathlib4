/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Homology.ShortComplex.Exact
import Mathlib.CategoryTheory.ComposableArrows

/-!
# Exact sequences

A sequence of `n` composable arrows `S : ComposableArrows C` (i.e. a functor
`S : Fin (n + 1) ‚•§ C`) is said to be exact (`S.Exact`) if the composition
of two consecutive arrows are zero (`S.IsComplex`) and the diagram is
exact at each `i` for `1 ‚â§ i < n`.

Together with the inductive construction of composable arrows
`ComposableArrows.precomp`, this is useful in order to state that certain
finite sequences of morphisms are exact (e.g the snake lemma), even though
in the applications it would usually be more convenient to use individual
lemmas expressing the exactness at a particular object.

This implementation is a refactor of `exact_seq` with appeared in the
Liquid Tensor Experiment as a property of lists in `Arrow C`.

-/

namespace CategoryTheory

open Limits

variable {C : Type*} [Category C] [HasZeroMorphisms C]

/-- The composable arrows associated to a short complex. -/
@[simps!]
def ShortComplex.toComposableArrows (S : ShortComplex C) : ComposableArrows C 2 :=
  ComposableArrows.mk‚ÇÇ S.f S.g

/-- A map of short complexes induces a map of composable arrows with the same data. -/
def ShortComplex.mapToComposableArrows {S‚ÇÅ S‚ÇÇ : ShortComplex C} (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) :
    S‚ÇÅ.toComposableArrows ‚ü∂ S‚ÇÇ.toComposableArrows :=
  ComposableArrows.homMk‚ÇÇ œÜ.œÑ‚ÇÅ œÜ.œÑ‚ÇÇ œÜ.œÑ‚ÇÉ œÜ.comm‚ÇÅ‚ÇÇ.symm œÜ.comm‚ÇÇ‚ÇÉ.symm

@[simp]
theorem ShortComplex.mapToComposableArrows_app_0 {S‚ÇÅ S‚ÇÇ : ShortComplex C} (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) :
    (ShortComplex.mapToComposableArrows œÜ).app 0 = œÜ.œÑ‚ÇÅ := rfl

@[simp]
theorem ShortComplex.mapToComposableArrows_app_1 {S‚ÇÅ S‚ÇÇ : ShortComplex C} (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) :
    (ShortComplex.mapToComposableArrows œÜ).app 1 = œÜ.œÑ‚ÇÇ := rfl

@[simp]
theorem ShortComplex.mapToComposableArrows_app_2 {S‚ÇÅ S‚ÇÇ : ShortComplex C} (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) :
    (ShortComplex.mapToComposableArrows œÜ).app 2 = œÜ.œÑ‚ÇÉ := rfl

@[simp]
theorem ShortComplex.mapToComposableArrows_id {S‚ÇÅ : ShortComplex C} :
    (ShortComplex.mapToComposableArrows (ùüô S‚ÇÅ)) = ùüô S‚ÇÅ.toComposableArrows := by
  cat_disch

@[simp]
theorem ShortComplex.mapToComposableArrows_comp {S‚ÇÅ S‚ÇÇ S‚ÇÉ : ShortComplex C} (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ)
    (œà : S‚ÇÇ ‚ü∂ S‚ÇÉ) : ShortComplex.mapToComposableArrows (œÜ ‚â´ œà) =
      ShortComplex.mapToComposableArrows œÜ ‚â´ ShortComplex.mapToComposableArrows œà := by
  cat_disch

namespace ComposableArrows

variable {n : ‚Ñï} (S : ComposableArrows C n)

/-- `F : ComposableArrows C n` is a complex if all compositions of
two consecutive arrows are zero. -/
structure IsComplex : Prop where
  /-- the composition of two consecutive arrows is zero -/
  zero (i : ‚Ñï) (hi : i + 2 ‚â§ n := by omega) :
    S.map' i (i + 1) ‚â´ S.map' (i + 1) (i + 2) = 0

attribute [reassoc] IsComplex.zero

variable {S}

@[reassoc]
lemma IsComplex.zero' (hS : S.IsComplex) (i j k : ‚Ñï) (hij : i + 1 = j := by omega)
    (hjk : j + 1 = k := by omega) (hk : k ‚â§ n := by omega) :
    S.map' i j ‚â´ S.map' j k = 0 := by
  subst hij hjk
  exact hS.zero i hk

lemma isComplex_of_iso {S‚ÇÅ S‚ÇÇ : ComposableArrows C n} (e : S‚ÇÅ ‚âÖ S‚ÇÇ) (h‚ÇÅ : S‚ÇÅ.IsComplex) :
    S‚ÇÇ.IsComplex where
  zero i hi := by
    rw [‚Üê cancel_epi (ComposableArrows.app' e.hom i), comp_zero,
      ‚Üê NatTrans.naturality_assoc, ‚Üê NatTrans.naturality,
      reassoc_of% (h‚ÇÅ.zero i hi), zero_comp]

lemma isComplex_iff_of_iso {S‚ÇÅ S‚ÇÇ : ComposableArrows C n} (e : S‚ÇÅ ‚âÖ S‚ÇÇ) :
    S‚ÇÅ.IsComplex ‚Üî S‚ÇÇ.IsComplex :=
  ‚ü®isComplex_of_iso e, isComplex_of_iso e.symm‚ü©

lemma isComplex‚ÇÄ (S : ComposableArrows C 0) : S.IsComplex where
  zero i hi := by simp at hi

lemma isComplex‚ÇÅ (S : ComposableArrows C 1) : S.IsComplex where
  zero i hi := by omega

variable (S)

/-- The short complex consisting of maps `S.map' i j` and `S.map' j k` when we know
that `S : ComposableArrows C n` satisfies `S.IsComplex`. -/
abbrev sc' (hS : S.IsComplex) (i j k : ‚Ñï) (hij : i + 1 = j := by omega)
    (hjk : j + 1 = k := by omega) (hk : k ‚â§ n := by omega) :
    ShortComplex C :=
  ShortComplex.mk (S.map' i j) (S.map' j k) (hS.zero' i j k)

/-- The short complex consisting of maps `S.map' i (i + 1)` and `S.map' (i + 1) (i + 2)`
when we know that `S : ComposableArrows C n` satisfies `S.IsComplex`. -/
abbrev sc (hS : S.IsComplex) (i : ‚Ñï) (hi : i + 2 ‚â§ n := by omega) :
    ShortComplex C :=
  S.sc' hS i (i + 1) (i + 2)

/-- `F : ComposableArrows C n` is exact if it is a complex and that all short
complexes consisting of two consecutive arrows are exact. -/
structure Exact : Prop extends S.IsComplex where
  exact (i : ‚Ñï) (hi : i + 2 ‚â§ n := by omega) : (S.sc toIsComplex i).Exact

variable {S}

lemma Exact.exact' (hS : S.Exact) (i j k : ‚Ñï) (hij : i + 1 = j := by omega)
    (hjk : j + 1 = k := by omega) (hk : k ‚â§ n := by omega) :
    (S.sc' hS.toIsComplex i j k).Exact := by
  subst hij hjk
  exact hS.exact i hk

/-- Functoriality maps for `ComposableArrows.sc'`. -/
@[simps]
def sc'Map {S‚ÇÅ S‚ÇÇ : ComposableArrows C n} (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) (h‚ÇÅ : S‚ÇÅ.IsComplex) (h‚ÇÇ : S‚ÇÇ.IsComplex)
    (i j k : ‚Ñï) (hij : i + 1 = j := by omega)
    (hjk : j + 1 = k := by omega) (hk : k ‚â§ n := by omega) :
    S‚ÇÅ.sc' h‚ÇÅ i j k ‚ü∂ S‚ÇÇ.sc' h‚ÇÇ i j k where
  œÑ‚ÇÅ := œÜ.app _
  œÑ‚ÇÇ := œÜ.app _
  œÑ‚ÇÉ := œÜ.app _

/-- Functoriality maps for `ComposableArrows.sc`. -/
@[simps!]
def scMap {S‚ÇÅ S‚ÇÇ : ComposableArrows C n} (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) (h‚ÇÅ : S‚ÇÅ.IsComplex) (h‚ÇÇ : S‚ÇÇ.IsComplex)
    (i : ‚Ñï) (hi : i + 2 ‚â§ n := by omega) :
    S‚ÇÅ.sc h‚ÇÅ i ‚ü∂ S‚ÇÇ.sc h‚ÇÇ i :=
  sc'Map œÜ h‚ÇÅ h‚ÇÇ i (i + 1) (i + 2)

/-- The isomorphism `S‚ÇÅ.sc' _ i j k ‚âÖ S‚ÇÇ.sc' _ i j k` induced by an isomorphism `S‚ÇÅ ‚âÖ S‚ÇÇ`
in `ComposableArrows C n`. -/
@[simps]
def sc'MapIso {S‚ÇÅ S‚ÇÇ : ComposableArrows C n} (e : S‚ÇÅ ‚âÖ S‚ÇÇ)
    (h‚ÇÅ : S‚ÇÅ.IsComplex) (h‚ÇÇ : S‚ÇÇ.IsComplex) (i j k : ‚Ñï) (hij : i + 1 = j := by omega)
    (hjk : j + 1 = k := by omega) (hk : k ‚â§ n := by omega) :
    S‚ÇÅ.sc' h‚ÇÅ i j k ‚âÖ S‚ÇÇ.sc' h‚ÇÇ i j k where
  hom := sc'Map e.hom h‚ÇÅ h‚ÇÇ i j k
  inv := sc'Map e.inv h‚ÇÇ h‚ÇÅ i j k
  hom_inv_id := by ext <;> simp
  inv_hom_id := by ext <;> simp

/-- The isomorphism `S‚ÇÅ.sc _ i ‚âÖ S‚ÇÇ.sc _ i` induced by an isomorphism `S‚ÇÅ ‚âÖ S‚ÇÇ`
in `ComposableArrows C n`. -/
@[simps]
def scMapIso {S‚ÇÅ S‚ÇÇ : ComposableArrows C n} (e : S‚ÇÅ ‚âÖ S‚ÇÇ)
    (h‚ÇÅ : S‚ÇÅ.IsComplex) (h‚ÇÇ : S‚ÇÇ.IsComplex)
    (i : ‚Ñï) (hi : i + 2 ‚â§ n := by omega) :
    S‚ÇÅ.sc h‚ÇÅ i ‚âÖ S‚ÇÇ.sc h‚ÇÇ i where
  hom := scMap e.hom h‚ÇÅ h‚ÇÇ i
  inv := scMap e.inv h‚ÇÇ h‚ÇÅ i
  hom_inv_id := by ext <;> simp
  inv_hom_id := by ext <;> simp

lemma exact_of_iso {S‚ÇÅ S‚ÇÇ : ComposableArrows C n} (e : S‚ÇÅ ‚âÖ S‚ÇÇ) (h‚ÇÅ : S‚ÇÅ.Exact) :
    S‚ÇÇ.Exact where
  toIsComplex := isComplex_of_iso e h‚ÇÅ.toIsComplex
  exact i hi := ShortComplex.exact_of_iso (scMapIso e h‚ÇÅ.toIsComplex
    (isComplex_of_iso e h‚ÇÅ.toIsComplex) i) (h‚ÇÅ.exact i hi)

lemma exact_iff_of_iso {S‚ÇÅ S‚ÇÇ : ComposableArrows C n} (e : S‚ÇÅ ‚âÖ S‚ÇÇ) :
    S‚ÇÅ.Exact ‚Üî S‚ÇÇ.Exact :=
  ‚ü®exact_of_iso e, exact_of_iso e.symm‚ü©

lemma exact‚ÇÄ (S : ComposableArrows C 0) : S.Exact where
  toIsComplex := S.isComplex‚ÇÄ
  exact i hi := by simp at hi

lemma exact‚ÇÅ (S : ComposableArrows C 1) : S.Exact where
  toIsComplex := S.isComplex‚ÇÅ
  exact i hi := by exfalso; omega

lemma isComplex‚ÇÇ_iff (S : ComposableArrows C 2) :
    S.IsComplex ‚Üî S.map' 0 1 ‚â´ S.map' 1 2 = 0 := by
  constructor
  ¬∑ intro h
    exact h.zero 0 (by omega)
  ¬∑ intro h
    refine IsComplex.mk (fun i hi => ?_)
    obtain rfl : i = 0 := by omega
    exact h

lemma isComplex‚ÇÇ_mk (S : ComposableArrows C 2) (w : S.map' 0 1 ‚â´ S.map' 1 2 = 0) :
    S.IsComplex :=
  S.isComplex‚ÇÇ_iff.2 w

lemma _root_.CategoryTheory.ShortComplex.isComplex_toComposableArrows (S : ShortComplex C) :
    S.toComposableArrows.IsComplex :=
  -- Disable `Fin.reduceFinMk` because otherwise `Precompose.map_one_succ` does not apply. (https://github.com/leanprover-community/mathlib4/issues/27382)
  isComplex‚ÇÇ_mk _ (by simp [-Fin.reduceFinMk])

lemma exact‚ÇÇ_iff (S : ComposableArrows C 2) (hS : S.IsComplex) :
    S.Exact ‚Üî (S.sc' hS 0 1 2).Exact := by
  constructor
  ¬∑ intro h
    exact h.exact 0 (by omega)
  ¬∑ intro h
    refine Exact.mk hS (fun i hi => ?_)
    obtain rfl : i = 0 := by omega
    exact h

lemma exact‚ÇÇ_mk (S : ComposableArrows C 2) (w : S.map' 0 1 ‚â´ S.map' 1 2 = 0)
    (h : (ShortComplex.mk _ _ w).Exact) : S.Exact :=
  (S.exact‚ÇÇ_iff (S.isComplex‚ÇÇ_mk w)).2 h

lemma _root_.CategoryTheory.ShortComplex.Exact.exact_toComposableArrows
    {S : ShortComplex C} (hS : S.Exact) :
    S.toComposableArrows.Exact :=
  exact‚ÇÇ_mk _ _ hS

lemma _root_.CategoryTheory.ShortComplex.exact_iff_exact_toComposableArrows
    (S : ShortComplex C) :
    S.Exact ‚Üî S.toComposableArrows.Exact :=
  (S.toComposableArrows.exact‚ÇÇ_iff S.isComplex_toComposableArrows).symm

lemma exact_iff_Œ¥‚ÇÄ (S : ComposableArrows C (n + 2)) :
    S.Exact ‚Üî (mk‚ÇÇ (S.map' 0 1) (S.map' 1 2)).Exact ‚àß S.Œ¥‚ÇÄ.Exact := by
  constructor
  ¬∑ intro h
    constructor
    ¬∑ rw [exact‚ÇÇ_iff]; swap
      ¬∑ rw [isComplex‚ÇÇ_iff]
        exact h.toIsComplex.zero 0
      exact h.exact 0 (by omega)
    ¬∑ exact Exact.mk (IsComplex.mk (fun i hi => h.toIsComplex.zero (i + 1)))
        (fun i hi => h.exact (i + 1))
  ¬∑ rintro ‚ü®h, h‚ÇÄ‚ü©
    refine Exact.mk (IsComplex.mk (fun i hi => ?_)) (fun i hi => ?_)
    ¬∑ obtain _ | i := i
      ¬∑ exact h.toIsComplex.zero 0
      ¬∑ exact h‚ÇÄ.toIsComplex.zero i
    ¬∑ obtain _ | i := i
      ¬∑ exact h.exact 0
      ¬∑ exact h‚ÇÄ.exact i

lemma Exact.Œ¥‚ÇÄ {S : ComposableArrows C (n + 2)} (hS : S.Exact) :
    S.Œ¥‚ÇÄ.Exact := by
  rw [exact_iff_Œ¥‚ÇÄ] at hS
  exact hS.2

/-- If `S : ComposableArrows C (n + 2)` is such that the first two arrows form
an exact sequence and that the tail `S.Œ¥‚ÇÄ` is exact, then `S` is also exact.
See `ShortComplex.SnakeInput.snake_lemma` in `Algebra.Homology.ShortComplex.SnakeLemma`
for a use of this lemma. -/
lemma exact_of_Œ¥‚ÇÄ {S : ComposableArrows C (n + 2)}
    (h : (mk‚ÇÇ (S.map' 0 1) (S.map' 1 2)).Exact) (h‚ÇÄ : S.Œ¥‚ÇÄ.Exact) : S.Exact := by
  rw [exact_iff_Œ¥‚ÇÄ]
  constructor <;> assumption

lemma exact_iff_Œ¥last {n : ‚Ñï} (S : ComposableArrows C (n + 2)) :
    S.Exact ‚Üî S.Œ¥last.Exact ‚àß (mk‚ÇÇ (S.map' n (n + 1)) (S.map' (n + 1) (n + 2))).Exact := by
  constructor
  ¬∑ intro h
    constructor
    ¬∑ exact Exact.mk (IsComplex.mk (fun i hi => h.toIsComplex.zero i))
        (fun i hi => h.exact i)
    ¬∑ rw [exact‚ÇÇ_iff]; swap
      ¬∑ rw [isComplex‚ÇÇ_iff]
        exact h.toIsComplex.zero n
      exact h.exact n (by omega)
  ¬∑ rintro ‚ü®h, h'‚ü©
    refine Exact.mk (IsComplex.mk (fun i hi => ?_)) (fun i hi => ?_)
    ¬∑ simp only [Nat.add_le_add_iff_right] at hi
      obtain hi | rfl := hi.lt_or_eq
      ¬∑ exact h.toIsComplex.zero i
      ¬∑ exact h'.toIsComplex.zero 0
    ¬∑ simp only [Nat.add_le_add_iff_right] at hi
      obtain hi | rfl := hi.lt_or_eq
      ¬∑ exact h.exact i
      ¬∑ exact h'.exact 0

lemma Exact.Œ¥last {S : ComposableArrows C (n + 2)} (hS : S.Exact) :
    S.Œ¥last.Exact := by
  rw [exact_iff_Œ¥last] at hS
  exact hS.1

lemma exact_of_Œ¥last {n : ‚Ñï} (S : ComposableArrows C (n + 2))
    (h‚ÇÅ : S.Œ¥last.Exact) (h‚ÇÇ : (mk‚ÇÇ (S.map' n (n + 1)) (S.map' (n + 1) (n + 2))).Exact) :
    S.Exact := by
  rw [exact_iff_Œ¥last]
  constructor <;> assumption

lemma Exact.isIso_map' {C : Type*} [Category C] [Preadditive C]
    [Balanced C] {n : ‚Ñï} {S : ComposableArrows C n} (hS : S.Exact) (k : ‚Ñï) (hk : k + 3 ‚â§ n)
    (h‚ÇÄ : S.map' k (k + 1) = 0) (h‚ÇÅ : S.map' (k + 2) (k + 3) = 0) :
    IsIso (S.map' (k + 1) (k + 2)) := by
  have := (hS.exact k).mono_g h‚ÇÄ
  have := (hS.exact (k + 1)).epi_f h‚ÇÅ
  apply isIso_of_mono_of_epi

end ComposableArrows

end CategoryTheory
