/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Homology.ExactSequence

/-!
# Exact sequences with four terms

-/

namespace CategoryTheory

open Category Limits

namespace ComposableArrows

section HasZeroMorphisms

namespace IsComplex

variable {C : Type*} [Category C] [HasZeroMorphisms C] {n : ‚Ñï} {S : ComposableArrows C (n + 3)}
  (hS : S.IsComplex) (k : ‚Ñï)

section

variable (hk : k ‚â§ n) (cc : CokernelCofork (S.map' k (k + 1)))
  (kf : KernelFork (S.map' (k + 2) (k + 3))) (hcc : IsColimit cc) (hkf : IsLimit kf)

/-- Generalization of `cokerToKer`. -/
def cokerToKer' : cc.pt ‚ü∂ kf.pt :=
  IsColimit.desc hcc (CokernelCofork.ofœÄ _
    (show S.map' k (k + 1) ‚â´ IsLimit.lift hkf (KernelFork.ofŒπ _ (hS.zero (k + 1))) = 0 from
      Fork.IsLimit.hom_ext hkf (by simpa using hS.zero k)))

@[reassoc (attr := simp)]
lemma cokerToKer'_fac : cc.œÄ ‚â´ hS.cokerToKer' k hk cc kf hcc hkf ‚â´ kf.Œπ =
    S.map' (k + 1) (k + 2) := by
  simp [cokerToKer']

end

section

variable (hk : k ‚â§ n := by linarith)
  [HasCokernel (S.map' k (k + 1))] [HasKernel (S.map' (k + 2) (k + 3))]

noncomputable def cokerToKer :
    cokernel (S.map' k (k + 1) _ _) ‚ü∂ kernel (S.map' (k + 2) (k + 3) _ _) :=
  hS.cokerToKer' k hk (CokernelCofork.ofœÄ _ (cokernel.condition _))
    (KernelFork.ofŒπ _ (kernel.condition _)) (cokernelIsCokernel _) (kernelIsKernel _)

@[reassoc (attr := simp)]
lemma cokerToKer_fac :
    cokernel.œÄ _ ‚â´ hS.cokerToKer k ‚â´ kernel.Œπ _ = S.map' (k + 1) (k + 2) :=
  hS.cokerToKer'_fac k hk _ _ (cokernelIsCokernel _) (kernelIsKernel _)

end

section

variable (hk : k ‚â§ n := by linarith)

end


section

variable (hk : k ‚â§ n := by linarith)
  [(S.sc hS k).HasRightHomology] [(S.sc hS (k + 1)).HasLeftHomology]

noncomputable def opcyclesToCycles :
    (S.sc hS k _).opcycles ‚ü∂ (S.sc hS (k + 1) _).cycles :=
  hS.cokerToKer' k hk _ _ (S.sc hS k _).opcyclesIsCokernel
    (S.sc hS (k + 1) _).cyclesIsKernel

@[reassoc (attr := simp)]
lemma opcyclesToCycles_fac :
    (S.sc hS k _).pOpcycles ‚â´ hS.opcyclesToCycles k ‚â´ (S.sc hS (k + 1) _).iCycles =
      S.map' (k + 1) (k + 2) :=
  hS.cokerToKer'_fac k hk _ _ (S.sc hS k _).opcyclesIsCokernel
    (S.sc hS (k + 1) _).cyclesIsKernel

end

end IsComplex

end HasZeroMorphisms

section Preadditive

variable {C : Type*} [Category C] [Preadditive C] {n : ‚Ñï} {S : ComposableArrows C (n + 3)}

namespace IsComplex

variable (hS : S.IsComplex) (k : ‚Ñï) (hk : k ‚â§ n)
  (cc : CokernelCofork (S.map' k (k + 1))) (kf : KernelFork (S.map' (k + 2) (k + 3)))
  (hcc : IsColimit cc) (hkf : IsLimit kf)

/-- `cokerToKer'` is an epi. -/
lemma epi_cokerToKer' (hS' : (S.sc hS (k + 1)).Exact) :
    Epi (hS.cokerToKer' k hk cc kf hcc hkf) := by
  have := hS'.hasZeroObject
  have := hS'.hasHomology
  have : Epi cc.œÄ := ‚ü®fun _ _ => Cofork.IsColimit.hom_ext hcc‚ü©
  let h := hS'.leftHomologyDataOfIsLimitKernelFork kf hkf
  have := h.exact_iff_epi_f'.1 hS'
  have fac : cc.œÄ ‚â´ hS.cokerToKer' k hk cc kf hcc hkf = h.f' := by
    rw [‚Üê cancel_mono h.i, h.f'_i, ShortComplex.Exact.leftHomologyDataOfIsLimitKernelFork_i,
      assoc, IsComplex.cokerToKer'_fac]
  exact epi_of_epi_fac fac

/-- `cokerToKer'` is a mono. -/
lemma mono_cokerToKer' (hS' : (S.sc hS k).Exact) :
    Mono (hS.cokerToKer' k hk cc kf hcc hkf) := by
  have := hS'.hasZeroObject
  have := hS'.hasHomology
  have : Mono kf.Œπ := ‚ü®fun _ _ => Fork.IsLimit.hom_ext hkf‚ü©
  let h := hS'.rightHomologyDataOfIsColimitCokernelCofork cc hcc
  have := h.exact_iff_mono_g'.1 hS'
  have fac : hS.cokerToKer' k hk cc kf hcc hkf ‚â´ kf.Œπ = h.g' := by
    rw [‚Üê cancel_epi h.p, h.p_g', ShortComplex.Exact.rightHomologyDataOfIsColimitCokernelCofork_p,
      cokerToKer'_fac]
  exact mono_of_mono_fac fac

end IsComplex

end Preadditive

section Balanced

variable {C : Type*} [Category C] [Preadditive C] [Balanced C] {n : ‚Ñï}
  {S : ComposableArrows C (n + 3)} (hS : S.Exact)

namespace Exact

section

variable (k : ‚Ñï) (hk : k ‚â§ n)
  (cc : CokernelCofork (S.map' k (k + 1))) (kf : KernelFork (S.map' (k + 2) (k + 3)))
  (hcc : IsColimit cc) (hkf : IsLimit kf)

/-- Auxiliary definition for `cokerIsoKer'`. -/
def cokerToKer' : cc.pt ‚ü∂ kf.pt :=
  hS.toIsComplex.cokerToKer' k hk cc kf hcc hkf

omit [Balanced C] in
@[reassoc (attr := simp)]
lemma cokerToKer'_fac : cc.œÄ ‚â´ hS.cokerToKer' k hk cc kf hcc hkf ‚â´ kf.Œπ =
    S.map' (k + 1) (k + 2) := by
  simp [cokerToKer']

/-- `cokerToKer'` is an isomorphism. -/
instance isIso_cokerToKer' : IsIso (hS.cokerToKer' k hk cc kf hcc hkf) := by
  have : Mono (hS.cokerToKer' k hk cc kf hcc hkf) :=
      hS.toIsComplex.mono_cokerToKer' k hk cc kf hcc hkf
    (hS.exact k)
  have : Epi (hS.cokerToKer' k hk cc kf hcc hkf) :=
    hS.epi_cokerToKer' k hk cc kf hcc hkf (hS.exact (k + 1))
  apply isIso_of_mono_of_epi

/-- Auxiliary definition for `cokerIsoKer`. -/
@[simps! hom]
noncomputable def cokerIsoKer' : cc.pt ‚âÖ kf.pt :=
  asIso (hS.cokerToKer' k hk cc kf hcc hkf)

@[reassoc (attr := simp)]
lemma cokerIsoKer'_hom_inv_id :
    hS.cokerToKer' k hk cc kf hcc hkf ‚â´ (hS.cokerIsoKer' k hk cc kf hcc hkf).inv = ùüô _ :=
  (hS.cokerIsoKer' k hk cc kf hcc hkf).hom_inv_id

@[reassoc (attr := simp)]
lemma cokerIsoKer'_inv_hom_id :
    (hS.cokerIsoKer' k hk cc kf hcc hkf).inv ‚â´ hS.cokerToKer' k hk cc kf hcc hkf = ùüô _ :=
  (hS.cokerIsoKer' k hk cc kf hcc hkf).inv_hom_id

end

section

variable (k : ‚Ñï) (hk : k ‚â§ n := by linarith)
  [HasCokernel (S.map' k (k + 1))] [HasKernel (S.map' (k + 2) (k + 3))]

noncomputable def cokerIsoKer :
    cokernel (S.map' k (k + 1) _ _) ‚âÖ kernel (S.map' (k + 2) (k + 3) _ _) :=
  hS.cokerIsoKer' k hk (CokernelCofork.ofœÄ _ (cokernel.condition _))
    (KernelFork.ofŒπ _ (kernel.condition _)) (cokernelIsCokernel _) (kernelIsKernel _)

lemma cokerIsoKer_hom :
    (hS.cokerIsoKer k).hom = hS.toIsComplex.cokerToKer k := rfl

@[reassoc (attr := simp)]
lemma cokerIsoKer_hom_fac :
    cokernel.œÄ _ ‚â´ (hS.cokerIsoKer k).hom ‚â´ kernel.Œπ _ = S.map' (k + 1) (k + 2) := by
  rw [hS.cokerIsoKer_hom k, hS.toIsComplex.cokerToKer_fac k]

end

section

variable (k : ‚Ñï) (hk : k ‚â§ n := by linarith)
  [h‚ÇÅ : (hS.sc k).HasRightHomology] [h‚ÇÇ : (hS.sc (k + 1)).HasLeftHomology]

noncomputable def opcyclesIsoCycles :
    (hS.sc k _).opcycles ‚âÖ (hS.sc (k + 1) _).cycles :=
  hS.cokerIsoKer' k hk _ _ (hS.sc k _).opcyclesIsCokernel (hS.sc (k + 1) _).cyclesIsKernel

lemma opcyclesIsoCycles_hom :
    (hS.opcyclesIsoCycles k).hom = hS.toIsComplex.opcyclesToCycles k hk := rfl

@[reassoc (attr := simp)]
lemma opcyclesIsoCycles_hom_fac :
    (hS.sc k _).pOpcycles ‚â´ (hS.opcyclesIsoCycles k).hom ‚â´ (hS.sc (k + 1) _).iCycles =
      S.map' (k + 1) (k + 2) :=
  hS.toIsComplex.opcyclesToCycles_fac k hk

end

end Exact

end Balanced

end ComposableArrows

end CategoryTheory
