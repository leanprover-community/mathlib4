/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Algebra.Homology.Homology
import Mathlib.Algebra.Homology.Single
import Mathlib.CategoryTheory.Preadditive.AdditiveFunctor

#align_import algebra.homology.additive from "leanprover-community/mathlib"@"200eda15d8ff5669854ff6bcc10aaf37cb70498f"

/-!
# Homology is an additive functor

When `V` is preadditive, `HomologicalComplex V c` is also preadditive,
and `homologyFunctor` is additive.

TODO: similarly for `R`-linear.
-/


universe v u

open Classical

noncomputable section

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits HomologicalComplex

variable {Œπ : Type*}

variable {V : Type u} [Category.{v} V] [Preadditive V]

variable {c : ComplexShape Œπ} {C D E : HomologicalComplex V c}

variable (f g : C ‚ü∂ D) (h k : D ‚ü∂ E) (i : Œπ)

namespace HomologicalComplex

instance : Zero (C ‚ü∂ D) :=
  ‚ü®{ f := fun i => 0 }‚ü©

instance : Add (C ‚ü∂ D) :=
  ‚ü®fun f g => { f := fun i => f.f i + g.f i }‚ü©

instance : Neg (C ‚ü∂ D) :=
  ‚ü®fun f => { f := fun i => -f.f i }‚ü©

instance : Sub (C ‚ü∂ D) :=
  ‚ü®fun f g => { f := fun i => f.f i - g.f i }‚ü©

instance hasNatScalar : SMul ‚Ñï (C ‚ü∂ D) :=
  ‚ü®fun n f =>
    { f := fun i => n ‚Ä¢ f.f i
      comm' := fun i j _ => by simp [Preadditive.nsmul_comp, Preadditive.comp_nsmul] }‚ü©
#align homological_complex.has_nat_scalar HomologicalComplex.hasNatScalar

instance hasIntScalar : SMul ‚Ñ§ (C ‚ü∂ D) :=
  ‚ü®fun n f =>
    { f := fun i => n ‚Ä¢ f.f i
      comm' := fun i j _ => by simp [Preadditive.zsmul_comp, Preadditive.comp_zsmul] }‚ü©
#align homological_complex.has_int_scalar HomologicalComplex.hasIntScalar

@[simp]
theorem zero_f_apply (i : Œπ) : (0 : C ‚ü∂ D).f i = 0 :=
  rfl
#align homological_complex.zero_f_apply HomologicalComplex.zero_f_apply

@[simp]
theorem add_f_apply (f g : C ‚ü∂ D) (i : Œπ) : (f + g).f i = f.f i + g.f i :=
  rfl
#align homological_complex.add_f_apply HomologicalComplex.add_f_apply

@[simp]
theorem neg_f_apply (f : C ‚ü∂ D) (i : Œπ) : (-f).f i = -f.f i :=
  rfl
#align homological_complex.neg_f_apply HomologicalComplex.neg_f_apply

@[simp]
theorem sub_f_apply (f g : C ‚ü∂ D) (i : Œπ) : (f - g).f i = f.f i - g.f i :=
  rfl
#align homological_complex.sub_f_apply HomologicalComplex.sub_f_apply

@[simp]
theorem nsmul_f_apply (n : ‚Ñï) (f : C ‚ü∂ D) (i : Œπ) : (n ‚Ä¢ f).f i = n ‚Ä¢ f.f i :=
  rfl
#align homological_complex.nsmul_f_apply HomologicalComplex.nsmul_f_apply

@[simp]
theorem zsmul_f_apply (n : ‚Ñ§) (f : C ‚ü∂ D) (i : Œπ) : (n ‚Ä¢ f).f i = n ‚Ä¢ f.f i :=
  rfl
#align homological_complex.zsmul_f_apply HomologicalComplex.zsmul_f_apply

instance : AddCommGroup (C ‚ü∂ D) :=
  Function.Injective.addCommGroup Hom.f HomologicalComplex.hom_f_injective
    (by aesop_cat) (by aesop_cat) (by aesop_cat) (by aesop_cat) (by aesop_cat) (by aesop_cat)

-- porting note: proofs had to be provided here, otherwise Lean tries to apply
-- `Preadditive.add_comp/comp_add` to `HomologicalComplex V c`
instance : Preadditive (HomologicalComplex V c) where
  add_comp _ _ _ f f' g := by
    ext
    simp only [comp_f, add_f_apply]
    rw [Preadditive.add_comp]
  comp_add _ _ _ f g g' := by
    ext
    simp only [comp_f, add_f_apply]
    rw [Preadditive.comp_add]

/-- The `i`-th component of a chain map, as an additive map from chain maps to morphisms. -/
@[simps!]
def Hom.fAddMonoidHom {C‚ÇÅ C‚ÇÇ : HomologicalComplex V c} (i : Œπ) : (C‚ÇÅ ‚ü∂ C‚ÇÇ) ‚Üí+ (C‚ÇÅ.X i ‚ü∂ C‚ÇÇ.X i) :=
  AddMonoidHom.mk' (fun f => Hom.f f i) fun _ _ => rfl
#align homological_complex.hom.f_add_monoid_hom HomologicalComplex.Hom.fAddMonoidHom

end HomologicalComplex

namespace HomologicalComplex

instance eval_additive (i : Œπ) : (eval V c i).Additive where
#align homological_complex.eval_additive HomologicalComplex.eval_additive

instance cycles'_additive [HasEqualizers V] : (cycles'Functor V c i).Additive where
#align homological_complex.cycles_additive HomologicalComplex.cycles'_additive

variable [HasImages V] [HasImageMaps V]

instance boundaries_additive : (boundariesFunctor V c i).Additive where
#align homological_complex.boundaries_additive HomologicalComplex.boundaries_additive

variable [HasEqualizers V] [HasCokernels V]

instance homology_additive : (homology'Functor V c i).Additive where
  map_add {_ _ f g} := by
    dsimp [homology'Functor]
    ext
    simp only [homology'.œÄ_map, Preadditive.comp_add, ‚Üê Preadditive.add_comp]
    congr
    ext
    simp
#align homological_complex.homology_additive HomologicalComplex.homology_additive

end HomologicalComplex

namespace CategoryTheory

variable {W : Type*} [Category W] [Preadditive W]

/-- An additive functor induces a functor between homological complexes.
This is sometimes called the "prolongation".
-/
@[simps]
def Functor.mapHomologicalComplex (F : V ‚•§ W) [F.Additive] (c : ComplexShape Œπ) :
    HomologicalComplex V c ‚•§ HomologicalComplex W c where
  obj C :=
    { X := fun i => F.obj (C.X i)
      d := fun i j => F.map (C.d i j)
      shape := fun i j w => by
        dsimp only
        rw [C.shape _ _ w, F.map_zero]
      d_comp_d' := fun i j k _ _ => by rw [‚Üê F.map_comp, C.d_comp_d, F.map_zero] }
  map f :=
    { f := fun i => F.map (f.f i)
      comm' := fun i j _ => by
        dsimp
        rw [‚Üê F.map_comp, ‚Üê F.map_comp, f.comm] }
#align category_theory.functor.map_homological_complex CategoryTheory.Functor.mapHomologicalComplex

variable (V)

/-- The functor on homological complexes induced by the identity functor is
isomorphic to the identity functor. -/
@[simps!]
def Functor.mapHomologicalComplexIdIso (c : ComplexShape Œπ) :
    (ùü≠ V).mapHomologicalComplex c ‚âÖ ùü≠ _ :=
  NatIso.ofComponents fun K => Hom.isoOfComponents fun i => Iso.refl _
#align category_theory.functor.map_homological_complex_id_iso CategoryTheory.Functor.mapHomologicalComplexIdIso

variable {V}

instance Functor.map_homogical_complex_additive (F : V ‚•§ W) [F.Additive] (c : ComplexShape Œπ) :
    (F.mapHomologicalComplex c).Additive where
#align category_theory.functor.map_homogical_complex_additive CategoryTheory.Functor.map_homogical_complex_additive

instance Functor.mapHomologicalComplex_reflects_iso (F : V ‚•§ W) [F.Additive]
    [ReflectsIsomorphisms F] (c : ComplexShape Œπ) :
    ReflectsIsomorphisms (F.mapHomologicalComplex c) :=
  ‚ü®fun f => by
    intro
    haveI : ‚àÄ n : Œπ, IsIso (F.map (f.f n)) := fun n =>
      IsIso.of_iso
        ((HomologicalComplex.eval W c n).mapIso (asIso ((F.mapHomologicalComplex c).map f)))
    haveI := fun n => isIso_of_reflects_iso (f.f n) F
    exact HomologicalComplex.Hom.isIso_of_components f‚ü©
#align category_theory.functor.map_homological_complex_reflects_iso CategoryTheory.Functor.mapHomologicalComplex_reflects_iso

/-- A natural transformation between functors induces a natural transformation
between those functors applied to homological complexes.
-/
@[simps]
def NatTrans.mapHomologicalComplex {F G : V ‚•§ W} [F.Additive] [G.Additive] (Œ± : F ‚ü∂ G)
    (c : ComplexShape Œπ) : F.mapHomologicalComplex c ‚ü∂ G.mapHomologicalComplex c where
  app C := { f := fun i => Œ±.app _ }
#align category_theory.nat_trans.map_homological_complex CategoryTheory.NatTrans.mapHomologicalComplex

@[simp]
theorem NatTrans.mapHomologicalComplex_id (c : ComplexShape Œπ) (F : V ‚•§ W) [F.Additive] :
    NatTrans.mapHomologicalComplex (ùüô F) c = ùüô (F.mapHomologicalComplex c) := by aesop_cat
#align category_theory.nat_trans.map_homological_complex_id CategoryTheory.NatTrans.mapHomologicalComplex_id

@[simp]
theorem NatTrans.mapHomologicalComplex_comp (c : ComplexShape Œπ) {F G H : V ‚•§ W} [F.Additive]
    [G.Additive] [H.Additive] (Œ± : F ‚ü∂ G) (Œ≤ : G ‚ü∂ H) :
    NatTrans.mapHomologicalComplex (Œ± ‚â´ Œ≤) c =
      NatTrans.mapHomologicalComplex Œ± c ‚â´ NatTrans.mapHomologicalComplex Œ≤ c :=
  by aesop_cat
#align category_theory.nat_trans.map_homological_complex_comp CategoryTheory.NatTrans.mapHomologicalComplex_comp

@[reassoc (attr := simp 1100)]
theorem NatTrans.mapHomologicalComplex_naturality {c : ComplexShape Œπ} {F G : V ‚•§ W} [F.Additive]
    [G.Additive] (Œ± : F ‚ü∂ G) {C D : HomologicalComplex V c} (f : C ‚ü∂ D) :
    (F.mapHomologicalComplex c).map f ‚â´ (NatTrans.mapHomologicalComplex Œ± c).app D =
      (NatTrans.mapHomologicalComplex Œ± c).app C ‚â´ (G.mapHomologicalComplex c).map f :=
  by aesop_cat
#align category_theory.nat_trans.map_homological_complex_naturality CategoryTheory.NatTrans.mapHomologicalComplex_naturality

/-- A natural isomorphism between functors induces a natural isomorphism
between those functors applied to homological complexes.
-/
@[simps!]
def NatIso.mapHomologicalComplex {F G : V ‚•§ W} [F.Additive] [G.Additive] (Œ± : F ‚âÖ G)
    (c : ComplexShape Œπ) : F.mapHomologicalComplex c ‚âÖ G.mapHomologicalComplex c where
  hom := NatTrans.mapHomologicalComplex Œ±.hom c
  inv := NatTrans.mapHomologicalComplex Œ±.inv c
  hom_inv_id := by simp only [‚Üê NatTrans.mapHomologicalComplex_comp, Œ±.hom_inv_id,
    NatTrans.mapHomologicalComplex_id]
  inv_hom_id := by simp only [‚Üê NatTrans.mapHomologicalComplex_comp, Œ±.inv_hom_id,
    NatTrans.mapHomologicalComplex_id]
#align category_theory.nat_iso.map_homological_complex CategoryTheory.NatIso.mapHomologicalComplex

/-- An equivalence of categories induces an equivalences between the respective categories
of homological complex.
-/
@[simps]
def Equivalence.mapHomologicalComplex (e : V ‚âå W) [e.functor.Additive] (c : ComplexShape Œπ) :
    HomologicalComplex V c ‚âå HomologicalComplex W c where
  functor := e.functor.mapHomologicalComplex c
  inverse := e.inverse.mapHomologicalComplex c
  unitIso :=
    (Functor.mapHomologicalComplexIdIso V c).symm ‚â™‚â´ NatIso.mapHomologicalComplex e.unitIso c
  counitIso := NatIso.mapHomologicalComplex e.counitIso c ‚â™‚â´ Functor.mapHomologicalComplexIdIso W c
#align category_theory.equivalence.map_homological_complex CategoryTheory.Equivalence.mapHomologicalComplex

end CategoryTheory

namespace ChainComplex

variable {W : Type*} [Category W] [Preadditive W]

variable {Œ± : Type*} [AddRightCancelSemigroup Œ±] [One Œ±] [DecidableEq Œ±]

theorem map_chain_complex_of (F : V ‚•§ W) [F.Additive] (X : Œ± ‚Üí V) (d : ‚àÄ n, X (n + 1) ‚ü∂ X n)
    (sq : ‚àÄ n, d (n + 1) ‚â´ d n = 0) :
    (F.mapHomologicalComplex _).obj (ChainComplex.of X d sq) =
      ChainComplex.of (fun n => F.obj (X n)) (fun n => F.map (d n)) fun n => by
        rw [‚Üê F.map_comp, sq n, Functor.map_zero] := by
  refine' HomologicalComplex.ext rfl _
  rintro i j (rfl : j + 1 = i)
  simp only [CategoryTheory.Functor.mapHomologicalComplex_obj_d, of_d, eqToHom_refl, comp_id,
    id_comp]
#align chain_complex.map_chain_complex_of ChainComplex.map_chain_complex_of

end ChainComplex

variable [HasZeroObject V] {W : Type*} [Category W] [Preadditive W] [HasZeroObject W]

namespace HomologicalComplex

attribute [local simp] eqToHom_map

/-- Turning an object into a complex supported at `j` then applying a functor is
the same as applying the functor then forming the complex.
-/
def singleMapHomologicalComplex (F : V ‚•§ W) [F.Additive] (c : ComplexShape Œπ) (j : Œπ) :
    single V c j ‚ãô F.mapHomologicalComplex _ ‚âÖ F ‚ãô single W c j :=
  NatIso.ofComponents
    (fun X =>
      { hom := { f := fun i => if h : i = j then eqToHom (by simp [h]) else 0 }
        inv := { f := fun i => if h : i = j then eqToHom (by simp [h]) else 0 }
        hom_inv_id := by
          ext i
          dsimp
          split_ifs with h
          ¬∑ simp [h]
          ¬∑ rw [zero_comp, if_neg h]
            exact (zero_of_source_iso_zero _ F.mapZeroObject).symm
        inv_hom_id := by
          ext i
          dsimp
          split_ifs with h
          ¬∑ simp [h]
          ¬∑ rw [zero_comp, if_neg h]
            simp })
    fun f => by
    ext i
    dsimp
    split_ifs with h <;> simp [h]
#align homological_complex.single_map_homological_complex HomologicalComplex.singleMapHomologicalComplex

variable (F : V ‚•§ W) [Functor.Additive F] (c)

@[simp]
theorem singleMapHomologicalComplex_hom_app_self (j : Œπ) (X : V) :
    ((singleMapHomologicalComplex F c j).hom.app X).f j = eqToHom (by simp) := by
  simp [singleMapHomologicalComplex]
#align homological_complex.single_map_homological_complex_hom_app_self HomologicalComplex.singleMapHomologicalComplex_hom_app_self

@[simp]
theorem singleMapHomologicalComplex_hom_app_ne {i j : Œπ} (h : i ‚â† j) (X : V) :
    ((singleMapHomologicalComplex F c j).hom.app X).f i = 0 := by
  simp [singleMapHomologicalComplex, h]
#align homological_complex.single_map_homological_complex_hom_app_ne HomologicalComplex.singleMapHomologicalComplex_hom_app_ne

@[simp]
theorem singleMapHomologicalComplex_inv_app_self (j : Œπ) (X : V) :
    ((singleMapHomologicalComplex F c j).inv.app X).f j = eqToHom (by simp) := by
  simp [singleMapHomologicalComplex]
#align homological_complex.single_map_homological_complex_inv_app_self HomologicalComplex.singleMapHomologicalComplex_inv_app_self

@[simp]
theorem singleMapHomologicalComplex_inv_app_ne {i j : Œπ} (h : i ‚â† j) (X : V) :
    ((singleMapHomologicalComplex F c j).inv.app X).f i = 0 := by
  simp [singleMapHomologicalComplex, h]
#align homological_complex.single_map_homological_complex_inv_app_ne HomologicalComplex.singleMapHomologicalComplex_inv_app_ne

end HomologicalComplex

namespace ChainComplex

/-- Turning an object into a chain complex supported at zero then applying a functor is
the same as applying the functor then forming the complex.
-/
def single‚ÇÄMapHomologicalComplex (F : V ‚•§ W) [F.Additive] :
    single‚ÇÄ V ‚ãô F.mapHomologicalComplex _ ‚âÖ F ‚ãô single‚ÇÄ W :=
  NatIso.ofComponents
    (fun X =>
      { hom :=
          { f := fun i =>
              match i with
              | 0 => ùüô _
              | _ + 1 => F.mapZeroObject.hom }
        inv :=
          { f := fun i =>
              match i with
              | 0 => ùüô _
              | _ + 1 => F.mapZeroObject.inv }
        hom_inv_id := by
          ext (_|_)
          ¬∑ simp
          ¬∑ exact IsZero.eq_of_src (IsZero.of_iso (isZero_zero _) F.mapZeroObject) _ _
        inv_hom_id := by
          ext (_|_)
          ¬∑ simp
          ¬∑ exact IsZero.eq_of_src (isZero_zero _) _ _ })
    fun f => by ext (_|_) <;> simp
#align chain_complex.single‚ÇÄ_map_homological_complex ChainComplex.single‚ÇÄMapHomologicalComplex

@[simp]
theorem single‚ÇÄMapHomologicalComplex_hom_app_zero (F : V ‚•§ W) [F.Additive] (X : V) :
    ((single‚ÇÄMapHomologicalComplex F).hom.app X).f 0 = ùüô _ :=
  rfl
#align chain_complex.single‚ÇÄ_map_homological_complex_hom_app_zero ChainComplex.single‚ÇÄMapHomologicalComplex_hom_app_zero

@[simp]
theorem single‚ÇÄMapHomologicalComplex_hom_app_succ (F : V ‚•§ W) [F.Additive] (X : V) (n : ‚Ñï) :
    ((single‚ÇÄMapHomologicalComplex F).hom.app X).f (n + 1) = 0 :=
  rfl
#align chain_complex.single‚ÇÄ_map_homological_complex_hom_app_succ ChainComplex.single‚ÇÄMapHomologicalComplex_hom_app_succ

@[simp]
theorem single‚ÇÄMapHomologicalComplex_inv_app_zero (F : V ‚•§ W) [F.Additive] (X : V) :
    ((single‚ÇÄMapHomologicalComplex F).inv.app X).f 0 = ùüô _ :=
  rfl
#align chain_complex.single‚ÇÄ_map_homological_complex_inv_app_zero ChainComplex.single‚ÇÄMapHomologicalComplex_inv_app_zero

@[simp]
theorem single‚ÇÄMapHomologicalComplex_inv_app_succ (F : V ‚•§ W) [F.Additive] (X : V) (n : ‚Ñï) :
    ((single‚ÇÄMapHomologicalComplex F).inv.app X).f (n + 1) = 0 :=
  rfl
#align chain_complex.single‚ÇÄ_map_homological_complex_inv_app_succ ChainComplex.single‚ÇÄMapHomologicalComplex_inv_app_succ

end ChainComplex

namespace CochainComplex

/-- Turning an object into a cochain complex supported at zero then applying a functor is
the same as applying the functor then forming the cochain complex.
-/
def single‚ÇÄMapHomologicalComplex (F : V ‚•§ W) [F.Additive] :
    single‚ÇÄ V ‚ãô F.mapHomologicalComplex _ ‚âÖ F ‚ãô single‚ÇÄ W :=
  NatIso.ofComponents
    (fun X =>
      { hom :=
          { f := fun i =>
              match i with
              | 0 => ùüô _
              | _ + 1 => F.mapZeroObject.hom }
        inv :=
          { f := fun i =>
              match i with
              | 0 => ùüô _
              | _ + 1 => F.mapZeroObject.inv }
        hom_inv_id := by
          ext (_|_)
          ¬∑ simp
          ¬∑ exact IsZero.eq_of_src (IsZero.of_iso (isZero_zero _) F.mapZeroObject) _ _
        inv_hom_id := by
          ext (_|_)
          ¬∑ simp
          ¬∑ exact IsZero.eq_of_src (isZero_zero _) _ _ })
    fun f => by ext (_|_) <;> simp
#align cochain_complex.single‚ÇÄ_map_homological_complex CochainComplex.single‚ÇÄMapHomologicalComplex

@[simp]
theorem single‚ÇÄMapHomologicalComplex_hom_app_zero (F : V ‚•§ W) [F.Additive] (X : V) :
    ((single‚ÇÄMapHomologicalComplex F).hom.app X).f 0 = ùüô _ :=
  rfl
#align cochain_complex.single‚ÇÄ_map_homological_complex_hom_app_zero CochainComplex.single‚ÇÄMapHomologicalComplex_hom_app_zero

@[simp]
theorem single‚ÇÄMapHomologicalComplex_hom_app_succ (F : V ‚•§ W) [F.Additive] (X : V) (n : ‚Ñï) :
    ((single‚ÇÄMapHomologicalComplex F).hom.app X).f (n + 1) = 0 :=
  rfl
#align cochain_complex.single‚ÇÄ_map_homological_complex_hom_app_succ CochainComplex.single‚ÇÄMapHomologicalComplex_hom_app_succ

@[simp]
theorem single‚ÇÄMapHomologicalComplex_inv_app_zero (F : V ‚•§ W) [F.Additive] (X : V) :
    ((single‚ÇÄMapHomologicalComplex F).inv.app X).f 0 = ùüô _ :=
  rfl
#align cochain_complex.single‚ÇÄ_map_homological_complex_inv_app_zero CochainComplex.single‚ÇÄMapHomologicalComplex_inv_app_zero

@[simp]
theorem single‚ÇÄMapHomologicalComplex_inv_app_succ (F : V ‚•§ W) [F.Additive] (X : V) (n : ‚Ñï) :
    ((single‚ÇÄMapHomologicalComplex F).inv.app X).f (n + 1) = 0 :=
  rfl
#align cochain_complex.single‚ÇÄ_map_homological_complex_inv_app_succ CochainComplex.single‚ÇÄMapHomologicalComplex_inv_app_succ

end CochainComplex
