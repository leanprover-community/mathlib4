/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Algebra.Homology.Homology
import Mathlib.Algebra.Homology.Single
import Mathlib.CategoryTheory.Preadditive.AdditiveFunctor

#align_import algebra.homology.additive from "leanprover-community/mathlib"@"200eda15d8ff5669854ff6bcc10aaf37cb70498f"

/-!
# Homology is an additive functor

When `V` is preadditive, `HomologicalComplex V c` is also preadditive,
and `homologyFunctor` is additive.

-/


universe v u

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits HomologicalComplex

variable {Î¹ : Type*}
variable {V : Type u} [Category.{v} V] [Preadditive V]
variable {c : ComplexShape Î¹} {C D E : HomologicalComplex V c}
variable (f g : C âŸ¶ D) (h k : D âŸ¶ E) (i : Î¹)

namespace HomologicalComplex

instance : Zero (C âŸ¶ D) :=
  âŸ¨{ f := fun i => 0 }âŸ©

instance : Add (C âŸ¶ D) :=
  âŸ¨fun f g => { f := fun i => f.f i + g.f i }âŸ©

instance : Neg (C âŸ¶ D) :=
  âŸ¨fun f => { f := fun i => -f.f i }âŸ©

instance : Sub (C âŸ¶ D) :=
  âŸ¨fun f g => { f := fun i => f.f i - g.f i }âŸ©

instance hasNatScalar : SMul â„• (C âŸ¶ D) :=
  âŸ¨fun n f =>
    { f := fun i => n â€¢ f.f i
      comm' := fun i j _ => by simp [Preadditive.nsmul_comp, Preadditive.comp_nsmul] }âŸ©
#align homological_complex.has_nat_scalar HomologicalComplex.hasNatScalar

instance hasIntScalar : SMul â„¤ (C âŸ¶ D) :=
  âŸ¨fun n f =>
    { f := fun i => n â€¢ f.f i
      comm' := fun i j _ => by simp [Preadditive.zsmul_comp, Preadditive.comp_zsmul] }âŸ©
#align homological_complex.has_int_scalar HomologicalComplex.hasIntScalar

@[simp]
theorem zero_f_apply (i : Î¹) : (0 : C âŸ¶ D).f i = 0 :=
  rfl
#align homological_complex.zero_f_apply HomologicalComplex.zero_f_apply

@[simp]
theorem add_f_apply (f g : C âŸ¶ D) (i : Î¹) : (f + g).f i = f.f i + g.f i :=
  rfl
#align homological_complex.add_f_apply HomologicalComplex.add_f_apply

@[simp]
theorem neg_f_apply (f : C âŸ¶ D) (i : Î¹) : (-f).f i = -f.f i :=
  rfl
#align homological_complex.neg_f_apply HomologicalComplex.neg_f_apply

@[simp]
theorem sub_f_apply (f g : C âŸ¶ D) (i : Î¹) : (f - g).f i = f.f i - g.f i :=
  rfl
#align homological_complex.sub_f_apply HomologicalComplex.sub_f_apply

@[simp]
theorem nsmul_f_apply (n : â„•) (f : C âŸ¶ D) (i : Î¹) : (n â€¢ f).f i = n â€¢ f.f i :=
  rfl
#align homological_complex.nsmul_f_apply HomologicalComplex.nsmul_f_apply

@[simp]
theorem zsmul_f_apply (n : â„¤) (f : C âŸ¶ D) (i : Î¹) : (n â€¢ f).f i = n â€¢ f.f i :=
  rfl
#align homological_complex.zsmul_f_apply HomologicalComplex.zsmul_f_apply

instance : AddCommGroup (C âŸ¶ D) :=
  Function.Injective.addCommGroup Hom.f HomologicalComplex.hom_f_injective
    (by aesop_cat) (by aesop_cat) (by aesop_cat) (by aesop_cat) (by aesop_cat) (by aesop_cat)

-- Porting note: proofs had to be provided here, otherwise Lean tries to apply
-- `Preadditive.add_comp/comp_add` to `HomologicalComplex V c`
instance : Preadditive (HomologicalComplex V c) where
  add_comp _ _ _ f f' g := by
    ext
    simp only [comp_f, add_f_apply]
    rw [Preadditive.add_comp]
  comp_add _ _ _ f g g' := by
    ext
    simp only [comp_f, add_f_apply]
    rw [Preadditive.comp_add]

/-- The `i`-th component of a chain map, as an additive map from chain maps to morphisms. -/
@[simps!]
def Hom.fAddMonoidHom {Câ‚ Câ‚‚ : HomologicalComplex V c} (i : Î¹) : (Câ‚ âŸ¶ Câ‚‚) â†’+ (Câ‚.X i âŸ¶ Câ‚‚.X i) :=
  AddMonoidHom.mk' (fun f => Hom.f f i) fun _ _ => rfl
#align homological_complex.hom.f_add_monoid_hom HomologicalComplex.Hom.fAddMonoidHom

end HomologicalComplex

namespace HomologicalComplex

instance eval_additive (i : Î¹) : (eval V c i).Additive where
#align homological_complex.eval_additive HomologicalComplex.eval_additive

instance cycles'_additive [HasEqualizers V] : (cycles'Functor V c i).Additive where
#align homological_complex.cycles_additive HomologicalComplex.cycles'_additive

variable [HasImages V] [HasImageMaps V]

instance boundaries_additive : (boundariesFunctor V c i).Additive where
#align homological_complex.boundaries_additive HomologicalComplex.boundaries_additive

variable [HasEqualizers V] [HasCokernels V]

instance homology_additive : (homology'Functor V c i).Additive where
  map_add {_ _ f g} := by
    dsimp [homology'Functor]
    ext
    simp only [homology'.Ï€_map, Preadditive.comp_add, â† Preadditive.add_comp]
    congr
    ext
    simp
#align homological_complex.homology_additive HomologicalComplex.homology_additive

end HomologicalComplex

namespace CategoryTheory

variable {W : Type*} [Category W] [Preadditive W]

/-- An additive functor induces a functor between homological complexes.
This is sometimes called the "prolongation".
-/
@[simps]
def Functor.mapHomologicalComplex (F : V â¥¤ W) [F.Additive] (c : ComplexShape Î¹) :
    HomologicalComplex V c â¥¤ HomologicalComplex W c where
  obj C :=
    { X := fun i => F.obj (C.X i)
      d := fun i j => F.map (C.d i j)
      shape := fun i j w => by
        dsimp only
        rw [C.shape _ _ w, F.map_zero]
      d_comp_d' := fun i j k _ _ => by rw [â† F.map_comp, C.d_comp_d, F.map_zero] }
  map f :=
    { f := fun i => F.map (f.f i)
      comm' := fun i j _ => by
        dsimp
        rw [â† F.map_comp, â† F.map_comp, f.comm] }
#align category_theory.functor.map_homological_complex CategoryTheory.Functor.mapHomologicalComplex

variable (V)

/-- The functor on homological complexes induced by the identity functor is
isomorphic to the identity functor. -/
@[simps!]
def Functor.mapHomologicalComplexIdIso (c : ComplexShape Î¹) :
    (ðŸ­ V).mapHomologicalComplex c â‰… ðŸ­ _ :=
  NatIso.ofComponents fun K => Hom.isoOfComponents fun i => Iso.refl _
#align category_theory.functor.map_homological_complex_id_iso CategoryTheory.Functor.mapHomologicalComplexIdIso

variable {V}

instance Functor.map_homogical_complex_additive (F : V â¥¤ W) [F.Additive] (c : ComplexShape Î¹) :
    (F.mapHomologicalComplex c).Additive where
#align category_theory.functor.map_homogical_complex_additive CategoryTheory.Functor.map_homogical_complex_additive

instance Functor.mapHomologicalComplex_reflects_iso (F : V â¥¤ W) [F.Additive]
    [ReflectsIsomorphisms F] (c : ComplexShape Î¹) :
    ReflectsIsomorphisms (F.mapHomologicalComplex c) :=
  âŸ¨fun f => by
    intro
    haveI : âˆ€ n : Î¹, IsIso (F.map (f.f n)) := fun n =>
      IsIso.of_iso
        ((HomologicalComplex.eval W c n).mapIso (asIso ((F.mapHomologicalComplex c).map f)))
    haveI := fun n => isIso_of_reflects_iso (f.f n) F
    exact HomologicalComplex.Hom.isIso_of_components fâŸ©
#align category_theory.functor.map_homological_complex_reflects_iso CategoryTheory.Functor.mapHomologicalComplex_reflects_iso

/-- A natural transformation between functors induces a natural transformation
between those functors applied to homological complexes.
-/
@[simps]
def NatTrans.mapHomologicalComplex {F G : V â¥¤ W} [F.Additive] [G.Additive] (Î± : F âŸ¶ G)
    (c : ComplexShape Î¹) : F.mapHomologicalComplex c âŸ¶ G.mapHomologicalComplex c where
  app C := { f := fun i => Î±.app _ }
#align category_theory.nat_trans.map_homological_complex CategoryTheory.NatTrans.mapHomologicalComplex

@[simp]
theorem NatTrans.mapHomologicalComplex_id (c : ComplexShape Î¹) (F : V â¥¤ W) [F.Additive] :
    NatTrans.mapHomologicalComplex (ðŸ™ F) c = ðŸ™ (F.mapHomologicalComplex c) := by aesop_cat
#align category_theory.nat_trans.map_homological_complex_id CategoryTheory.NatTrans.mapHomologicalComplex_id

@[simp]
theorem NatTrans.mapHomologicalComplex_comp (c : ComplexShape Î¹) {F G H : V â¥¤ W} [F.Additive]
    [G.Additive] [H.Additive] (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) :
    NatTrans.mapHomologicalComplex (Î± â‰« Î²) c =
      NatTrans.mapHomologicalComplex Î± c â‰« NatTrans.mapHomologicalComplex Î² c :=
  by aesop_cat
#align category_theory.nat_trans.map_homological_complex_comp CategoryTheory.NatTrans.mapHomologicalComplex_comp

@[reassoc (attr := simp 1100)]
theorem NatTrans.mapHomologicalComplex_naturality {c : ComplexShape Î¹} {F G : V â¥¤ W} [F.Additive]
    [G.Additive] (Î± : F âŸ¶ G) {C D : HomologicalComplex V c} (f : C âŸ¶ D) :
    (F.mapHomologicalComplex c).map f â‰« (NatTrans.mapHomologicalComplex Î± c).app D =
      (NatTrans.mapHomologicalComplex Î± c).app C â‰« (G.mapHomologicalComplex c).map f :=
  by aesop_cat
#align category_theory.nat_trans.map_homological_complex_naturality CategoryTheory.NatTrans.mapHomologicalComplex_naturality

/-- A natural isomorphism between functors induces a natural isomorphism
between those functors applied to homological complexes.
-/
@[simps!]
def NatIso.mapHomologicalComplex {F G : V â¥¤ W} [F.Additive] [G.Additive] (Î± : F â‰… G)
    (c : ComplexShape Î¹) : F.mapHomologicalComplex c â‰… G.mapHomologicalComplex c where
  hom := NatTrans.mapHomologicalComplex Î±.hom c
  inv := NatTrans.mapHomologicalComplex Î±.inv c
  hom_inv_id := by simp only [â† NatTrans.mapHomologicalComplex_comp, Î±.hom_inv_id,
    NatTrans.mapHomologicalComplex_id]
  inv_hom_id := by simp only [â† NatTrans.mapHomologicalComplex_comp, Î±.inv_hom_id,
    NatTrans.mapHomologicalComplex_id]
#align category_theory.nat_iso.map_homological_complex CategoryTheory.NatIso.mapHomologicalComplex

/-- An equivalence of categories induces an equivalences between the respective categories
of homological complex.
-/
@[simps]
def Equivalence.mapHomologicalComplex (e : V â‰Œ W) [e.functor.Additive] (c : ComplexShape Î¹) :
    HomologicalComplex V c â‰Œ HomologicalComplex W c where
  functor := e.functor.mapHomologicalComplex c
  inverse := e.inverse.mapHomologicalComplex c
  unitIso :=
    (Functor.mapHomologicalComplexIdIso V c).symm â‰ªâ‰« NatIso.mapHomologicalComplex e.unitIso c
  counitIso := NatIso.mapHomologicalComplex e.counitIso c â‰ªâ‰« Functor.mapHomologicalComplexIdIso W c
#align category_theory.equivalence.map_homological_complex CategoryTheory.Equivalence.mapHomologicalComplex

end CategoryTheory

namespace ChainComplex

variable {W : Type*} [Category W] [Preadditive W]
variable {Î± : Type*} [AddRightCancelSemigroup Î±] [One Î±] [DecidableEq Î±]

theorem map_chain_complex_of (F : V â¥¤ W) [F.Additive] (X : Î± â†’ V) (d : âˆ€ n, X (n + 1) âŸ¶ X n)
    (sq : âˆ€ n, d (n + 1) â‰« d n = 0) :
    (F.mapHomologicalComplex _).obj (ChainComplex.of X d sq) =
      ChainComplex.of (fun n => F.obj (X n)) (fun n => F.map (d n)) fun n => by
        rw [â† F.map_comp, sq n, Functor.map_zero] := by
  refine' HomologicalComplex.ext rfl _
  rintro i j (rfl : j + 1 = i)
  simp only [CategoryTheory.Functor.mapHomologicalComplex_obj_d, of_d, eqToHom_refl, comp_id,
    id_comp]
#align chain_complex.map_chain_complex_of ChainComplex.map_chain_complex_of

end ChainComplex

variable [HasZeroObject V] {W : Type*} [Category W] [Preadditive W] [HasZeroObject W]

namespace HomologicalComplex

/-- Turning an object into a complex supported at `j` then applying a functor is
the same as applying the functor then forming the complex.
-/
noncomputable def singleMapHomologicalComplex (F : V â¥¤ W) [F.Additive] (c : ComplexShape Î¹)
    [DecidableEq Î¹] (j : Î¹) :
    single V c j â‹™ F.mapHomologicalComplex _ â‰… F â‹™ single W c j :=
  NatIso.ofComponents
    (fun X =>
      { hom := { f := fun i => if h : i = j then eqToHom (by simp [h]) else 0 }
        inv := { f := fun i => if h : i = j then eqToHom (by simp [h]) else 0 }
        hom_inv_id := by
          ext i
          dsimp
          split_ifs with h
          Â· simp [h]
          Â· rw [zero_comp, â† F.map_id,
              (isZero_single_obj_X c j X _ h).eq_of_src (ðŸ™ _) 0, F.map_zero]
        inv_hom_id := by
          ext i
          dsimp
          split_ifs with h
          Â· simp [h]
          Â· apply (isZero_single_obj_X c j _ _ h).eq_of_src })
    fun f => by
      ext i
      dsimp
      split_ifs with h
      Â· subst h
        simp [single_map_f_self, singleObjXSelf, singleObjXIsoOfEq, eqToHom_map]
      Â· apply (isZero_single_obj_X c j _ _ h).eq_of_tgt
#align homological_complex.single_map_homological_complex HomologicalComplex.singleMapHomologicalComplex

variable (F : V â¥¤ W) [Functor.Additive F] (c) [DecidableEq Î¹]

@[simp]
theorem singleMapHomologicalComplex_hom_app_self (j : Î¹) (X : V) :
    ((singleMapHomologicalComplex F c j).hom.app X).f j =
      F.map (singleObjXSelf c j X).hom â‰« (singleObjXSelf c j (F.obj X)).inv := by
  simp [singleMapHomologicalComplex, singleObjXSelf, singleObjXIsoOfEq, eqToHom_map]
#align homological_complex.single_map_homological_complex_hom_app_self HomologicalComplex.singleMapHomologicalComplex_hom_app_self

@[simp]
theorem singleMapHomologicalComplex_hom_app_ne {i j : Î¹} (h : i â‰  j) (X : V) :
    ((singleMapHomologicalComplex F c j).hom.app X).f i = 0 := by
  simp [singleMapHomologicalComplex, h]
#align homological_complex.single_map_homological_complex_hom_app_ne HomologicalComplex.singleMapHomologicalComplex_hom_app_ne

@[simp]
theorem singleMapHomologicalComplex_inv_app_self (j : Î¹) (X : V) :
    ((singleMapHomologicalComplex F c j).inv.app X).f j =
      (singleObjXSelf c j (F.obj X)).hom â‰« F.map (singleObjXSelf c j X).inv := by
  simp [singleMapHomologicalComplex, singleObjXSelf, singleObjXIsoOfEq, eqToHom_map]
#align homological_complex.single_map_homological_complex_inv_app_self HomologicalComplex.singleMapHomologicalComplex_inv_app_self

@[simp]
theorem singleMapHomologicalComplex_inv_app_ne {i j : Î¹} (h : i â‰  j) (X : V) :
    ((singleMapHomologicalComplex F c j).inv.app X).f i = 0 := by
  simp [singleMapHomologicalComplex, h]
#align homological_complex.single_map_homological_complex_inv_app_ne HomologicalComplex.singleMapHomologicalComplex_inv_app_ne

end HomologicalComplex
