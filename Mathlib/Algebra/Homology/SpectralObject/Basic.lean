/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.Algebra.Homology.ExactSequence
public import Mathlib.CategoryTheory.ComposableArrows.One
public import Mathlib.CategoryTheory.ComposableArrows.Two

/-!
# Spectral objects in abelian categories

In this file, we introduce the category `SpectralObject C Œπ` of spectral
objects in an abelian category `C` indexed by the category `Œπ`.

## References
* [Jean-Louis Verdier, *Des cat√©gories d√©riv√©es des cat√©gories ab√©liennes*, II.4][verdier1996]

-/

@[expose] public section

namespace CategoryTheory

open Category Limits

namespace Abelian

variable (C Œπ : Type*) [Category C] [Category Œπ] [Abelian C]

open ComposableArrows

/-- A spectral object in an abelian category category `C` indexed by a category ``
consists of a functor `H : ComposableArrows Œπ 1 ‚•§ C`, and a
functorial long exact sequence
`‚ãØ ‚ü∂ (H n‚ÇÄ).obj (mk‚ÇÅ f) ‚ü∂ (H n‚ÇÄ).obj (mk‚ÇÅ (f ‚â´ g)) ‚ü∂ (H n‚ÇÄ).obj (mk‚ÇÅ g) ‚ü∂ (H n‚ÇÅ).obj (mk‚ÇÅ f) ‚ü∂ ‚ãØ`
when `n‚ÇÄ + 1 = n‚ÇÅ` and `f` and `g` are composable morphisms in `Œπ`. (This will be
shortened as `H^n‚ÇÄ(f) ‚ü∂ H^n‚ÇÄ(f ‚â´ g) ‚ü∂ H^n‚ÇÄ(g) ‚ü∂ H^n‚ÇÅ(f)` in the documentation.) -/
structure SpectralObject where
  /-- A sequence of functors from `ComposableArrows Œπ 1` to the abelian category.
  The image of `mk‚ÇÅ f` will be referred to as `H^n(f)` in the documentation. -/
  H (n : ‚Ñ§) : ComposableArrows Œπ 1 ‚•§ C
  /-- The connecting homomorphism of the spectral object. (Use `Œ¥` instead.) -/
  Œ¥' (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (h : n‚ÇÄ + 1 = n‚ÇÅ) :
    functorArrows Œπ 1 2 2 ‚ãô H n‚ÇÄ ‚ü∂ functorArrows Œπ 0 1 2 ‚ãô H n‚ÇÅ
  exact‚ÇÅ' (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (h : n‚ÇÄ + 1 = n‚ÇÅ) (D : ComposableArrows Œπ 2) :
    (mk‚ÇÇ ((Œ¥' n‚ÇÄ n‚ÇÅ h).app D) ((H n‚ÇÅ).map ((mapFunctorArrows Œπ 0 1 0 2 2).app D))).Exact
  exact‚ÇÇ' (n : ‚Ñ§) (D : ComposableArrows Œπ 2) :
    (mk‚ÇÇ ((H n).map ((mapFunctorArrows Œπ 0 1 0 2 2).app D))
      ((H n).map ((mapFunctorArrows Œπ 0 2 1 2 2).app D))).Exact
  exact‚ÇÉ' (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (h : n‚ÇÄ + 1 = n‚ÇÅ) (D : ComposableArrows Œπ 2) :
    (mk‚ÇÇ ((H n‚ÇÄ).map ((mapFunctorArrows Œπ 0 2 1 2 2).app D)) ((Œ¥' n‚ÇÄ n‚ÇÅ h).app D)).Exact

namespace SpectralObject

variable {C Œπ} (X : SpectralObject C Œπ)

section

variable (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ) {i j k : Œπ} (f : i ‚ü∂ j) (g : j ‚ü∂ k)

/-- The connecting homomorphism of the spectral object. -/
def Œ¥ : (X.H n‚ÇÄ).obj (mk‚ÇÅ g) ‚ü∂ (X.H n‚ÇÅ).obj (mk‚ÇÅ f) :=
  (X.Œ¥' n‚ÇÄ n‚ÇÅ hn‚ÇÅ).app (mk‚ÇÇ f g)

@[reassoc]
lemma Œ¥_naturality {i' j' k' : Œπ} (f' : i' ‚ü∂ j') (g' : j' ‚ü∂ k')
    (Œ± : mk‚ÇÅ f ‚ü∂ mk‚ÇÅ f') (Œ≤ : mk‚ÇÅ g ‚ü∂ mk‚ÇÅ g') (hŒ±Œ≤ : Œ±.app 1 = Œ≤.app 0 := by cat_disch) :
    (X.H n‚ÇÄ).map Œ≤ ‚â´ X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f' g' = X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚â´ (X.H n‚ÇÅ).map Œ± := by
  have h := (X.Œ¥' n‚ÇÄ n‚ÇÅ hn‚ÇÅ).naturality
    (homMk‚ÇÇ (Œ±.app 0) (Œ±.app 1) (Œ≤.app 1) (naturality' Œ± 0 1)
      (by simpa only [hŒ±Œ≤] using naturality' Œ≤ 0 1) : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f' g')
  dsimp at h
  convert h <;> cat_disch

end

section

variable (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ) {i j k : Œπ} (f : i ‚ü∂ j) (g : j ‚ü∂ k)
  (fg : i ‚ü∂ k) (h : f ‚â´ g = fg)

@[reassoc (attr := simp)]
lemma zero‚ÇÅ :
    X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚â´ (X.H n‚ÇÅ).map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f g fg h) = 0 := by
  subst h
  exact (X.exact‚ÇÅ' n‚ÇÄ n‚ÇÅ hn‚ÇÅ (mk‚ÇÇ f g)).zero 0

@[reassoc (attr := simp)]
lemma zero‚ÇÇ (fg : i ‚ü∂ k) (h : f ‚â´ g = fg) :
    (X.H n‚ÇÄ).map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f g fg h) ‚â´ (X.H n‚ÇÄ).map (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f g fg h) = 0 := by
  subst h
  exact (X.exact‚ÇÇ' n‚ÇÄ (mk‚ÇÇ f g)).zero 0

@[reassoc (attr := simp)]
lemma zero‚ÇÉ :
    (X.H n‚ÇÄ).map (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f g fg h) ‚â´ X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g = 0 := by
  subst h
  exact (X.exact‚ÇÉ' n‚ÇÄ n‚ÇÅ hn‚ÇÅ (mk‚ÇÇ f g)).zero 0

/-- The (exact) short complex `H^n‚ÇÄ(g) ‚ü∂ H^n‚ÇÅ(f) ‚ü∂ H^n‚ÇÅ(fg)` of a
spectral object, when `f ‚â´ g = fg` and `n‚ÇÄ + 1 = n‚ÇÅ`. -/
@[simps]
def sc‚ÇÅ : ShortComplex C :=
  ShortComplex.mk _ _ (X.zero‚ÇÅ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h)

/-- The (exact) short complex `H^n‚ÇÄ(f) ‚ü∂ H^n‚ÇÄ(fg) ‚ü∂ H^n‚ÇÄ(g)` of a
spectral object, when `f ‚â´ g = fg`. -/
@[simps]
def sc‚ÇÇ : ShortComplex C :=
  ShortComplex.mk _ _ (X.zero‚ÇÇ n‚ÇÄ f g fg h)

/-- The (exact) short complex `H^n‚ÇÄ(fg) ‚ü∂ H^n‚ÇÄ(g) ‚ü∂ H^n‚ÇÅ(f)`
of a spectral object, when `f ‚â´ g = fg` and `n‚ÇÄ + 1 = n‚ÇÅ`. -/
@[simps]
def sc‚ÇÉ : ShortComplex C :=
  ShortComplex.mk _ _ (X.zero‚ÇÉ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h)

lemma exact‚ÇÅ : (X.sc‚ÇÅ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h).Exact := by
  subst h
  exact (X.exact‚ÇÅ' n‚ÇÄ n‚ÇÅ hn‚ÇÅ (mk‚ÇÇ f g)).exact 0

lemma exact‚ÇÇ : (X.sc‚ÇÇ n‚ÇÄ f g fg h).Exact := by
  subst h
  exact (X.exact‚ÇÇ' n‚ÇÄ (mk‚ÇÇ f g)).exact 0

lemma exact‚ÇÉ : (X.sc‚ÇÉ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h).Exact := by
  subst h
  exact ((X.exact‚ÇÉ' n‚ÇÄ n‚ÇÅ hn‚ÇÅ (mk‚ÇÇ f g))).exact 0

/-- The (exact) sequence
`H^n‚ÇÄ(f) ‚ü∂ H^n‚ÇÄ(fg) ‚ü∂ H^n‚ÇÄ(g) ‚ü∂ H^n‚ÇÅ(f) ‚ü∂ H^n‚ÇÅ(fg) ‚ü∂ H^n‚ÇÅ(g)`
of a spectral object, when `f ‚â´ g = fg` and `n‚ÇÄ + 1 = n‚ÇÅ`. -/
abbrev composableArrows‚ÇÖ : ComposableArrows C 5 :=
  mk‚ÇÖ ((X.H n‚ÇÄ).map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f g fg h)) ((X.H n‚ÇÄ).map (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f g fg h))
    (X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g) ((X.H n‚ÇÅ).map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f g fg h))
    ((X.H n‚ÇÅ).map (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f g fg h))

lemma composableArrows‚ÇÖ_exact :
    (X.composableArrows‚ÇÖ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h).Exact :=
  exact_of_Œ¥‚ÇÄ (X.exact‚ÇÇ n‚ÇÄ _ _ _ h).exact_toComposableArrows
    (exact_of_Œ¥‚ÇÄ (X.exact‚ÇÉ n‚ÇÄ n‚ÇÅ hn‚ÇÅ _ _ _ h).exact_toComposableArrows
      (exact_of_Œ¥‚ÇÄ (X.exact‚ÇÅ n‚ÇÄ n‚ÇÅ hn‚ÇÅ _ _ _ h).exact_toComposableArrows
        ((X.exact‚ÇÇ n‚ÇÅ _ _ _ h).exact_toComposableArrows)))

end

@[reassoc (attr := simp)]
lemma Œ¥_Œ¥ (n‚ÇÄ n‚ÇÅ n‚ÇÇ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ) (hn‚ÇÇ : n‚ÇÅ + 1 = n‚ÇÇ)
    {i j k l : Œπ} (f : i ‚ü∂ j) (g : j ‚ü∂ k) (h : k ‚ü∂ l) :
    X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ g h ‚â´ X.Œ¥ n‚ÇÅ n‚ÇÇ hn‚ÇÇ f g = 0 := by
  have eq := X.Œ¥_naturality n‚ÇÅ n‚ÇÇ hn‚ÇÇ f g f (g ‚â´ h) (ùüô _) (twoŒ¥‚ÇÇToŒ¥‚ÇÅ g h _ rfl)
  rw [Functor.map_id, comp_id] at eq
  rw [‚Üê eq, X.zero‚ÇÅ_assoc n‚ÇÄ n‚ÇÅ hn‚ÇÅ g h _ rfl, zero_comp]

/-- The type of morphisms between spectral objects in abelian categories. -/
@[ext]
structure Hom (X' : SpectralObject C Œπ) where
  /-- The natural transformation that is part of a morphism between spectral objects. -/
  hom (n : ‚Ñ§) : X.H n ‚ü∂ X'.H n
  comm (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ) {i j k : Œπ} (f : i ‚ü∂ j) (g : j ‚ü∂ k) :
    X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚â´ (hom n‚ÇÅ).app (mk‚ÇÅ f) =
    (hom n‚ÇÄ).app (mk‚ÇÅ g) ‚â´ X'.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g := by cat_disch

attribute [reassoc (attr := simp)] Hom.comm

@[simps]
instance : Category (SpectralObject C Œπ) where
  Hom := Hom
  id X := { hom _ := ùüô _ }
  comp f g := { hom n := f.hom n ‚â´ g.hom n }

attribute [simp] id_hom
attribute [reassoc, simp] comp_hom

end SpectralObject

end Abelian

end CategoryTheory
