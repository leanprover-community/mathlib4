/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.Algebra.Homology.SpectralObject.Basic
public import Mathlib.Algebra.Homology.ExactSequenceFour

/-!
# Kernel and cokernel of the differentiel of a spectral object

Let `X` be a spectral object index by the category `Œπ`
in the abelian category `C`. In this file, we introduce
the kernel `X.cycles` and the cokernel `X.opcycles` of `X.Œ¥`.
These are defined when `f` and `g` are composable morphisms
in `Œπ` and for any integer `n`.
In the documentation, the kernel `X.cycles n f g` of
`Œ¥ : H^n(g) ‚ü∂ H^{n+1}(f)` shall be denoted `Z^n(f, g)`,
and the cokernel `X.opcycles n f g` of `Œ¥ : H^{n-1}(g) ‚ü∂ H^n(f)`
shall be denoted `opZ^n(f, g)`.
The definitions `cyclesMap` and `opcyclesMap` give the
functoriality of these definitions with respect
to morphisms in `ComposableArrows Œπ 2`.

We record that `Z^n(f, g)` is a kernel by the lemma
`kernelSequenceCycles_exact` and that `opZ^n(f, g)` is
a cokernel by the lemma `cokernelSequenceOpcycles_exact`.
We also provide a constructor `X.liftCycles` for morphisms
to cycles and `X.descOpcycles` for morphisms from opcycles.

The fact that the morphisms `Œ¥` are part of a long exact sequence allow
to show that `X.cycles` also identify to a cokernel (`cokernelIsoCycles`)
and `X.opcycles` to a kernel (`opcyclesIsoKernel`).
More precisely, the exactness of `H^n(f) ‚ü∂ H^n(f ‚â´ g) ‚ü∂ Z^n(f, g) ‚ü∂ 0`
is `cokernelSequenceCycles_exact` and the exactness of
`0 ‚ü∂ opZ^n(f, g) ‚ü∂ H^n(f ‚â´ g) ‚ü∂ H^n(g)` is
`kernelSequenceOpcycles_exact`. In particular, we also
get constructors `descCycles` and `liftOpcycles` for morphisms
from cycles and to opcycles.

When `f‚ÇÅ`, `f‚ÇÇ` and `f‚ÇÉ` are composable morphisms, we introduce
morphisms `Œ¥ToCycles : H^n(f‚ÇÉ) ‚ü∂ Z^{n+1}(f‚ÇÅ, f‚ÇÇ)` and .
`Œ¥FromOpcycles : opZ^n(f‚ÇÇ, f‚ÇÉ) ‚ü∂ H^{n+1}(f‚ÇÅ)`.

## References
* [Jean-Louis Verdier, *Des cat√©gories d√©riv√©es des cat√©gories ab√©liennes*, II.4][verdier1996]
-/

@[expose] public section

namespace CategoryTheory

open Limits ComposableArrows

namespace Abelian

variable {C Œπ : Type*} [Category C] [Category Œπ] [Abelian C]

namespace SpectralObject

variable (X : SpectralObject C Œπ)

section

variable (n : ‚Ñ§) {i j k : Œπ} (f : i ‚ü∂ j) (g : j ‚ü∂ k)

/-- The kernel of `Œ¥ : H^n(g) ‚ü∂ H^{n+1}(f)`. In the documentation,
this may be shortened as `Z^n(f, g)` -/
noncomputable def cycles : C := kernel (X.Œ¥ n (n + 1) rfl f g)

/-- The cokernel of `Œ¥ : H^{n-1}(g) ‚ü∂ H^n(g)`. In the documentation,
this may be shortened as `opZ^n‚ÇÅ(f, g)`. -/
noncomputable def opcycles : C := cokernel (X.Œ¥ (n - 1) n (by lia) f g)

/-- The inclusion `Z^n(f, g) ‚ü∂ H^n(g)` of the kernel of `Œ¥`. -/
noncomputable def iCycles :
    X.cycles n f g ‚ü∂ (X.H n).obj (mk‚ÇÅ g) :=
  kernel.Œπ _

/-- The projection `H^n(f) ‚ü∂ opZ^n(f, g)` to the cokernel of `Œ¥`. -/
noncomputable def pOpcycles :
    (X.H n).obj (mk‚ÇÅ f) ‚ü∂ X.opcycles n f g :=
  cokernel.œÄ _

instance : Mono (X.iCycles n f g) := by
  dsimp [iCycles]
  infer_instance

instance : Epi (X.pOpcycles n f g) := by
  dsimp [pOpcycles]
  infer_instance

lemma isZero_opcycles (h : IsZero ((X.H n).obj (mk‚ÇÅ f))) :
    IsZero (X.opcycles n f g) := by
  rw [IsZero.iff_id_eq_zero, ‚Üê cancel_epi (X.pOpcycles ..)]
  apply h.eq_of_src

lemma isZero_cycles (h : IsZero ((X.H n).obj (mk‚ÇÅ g))) :
    IsZero (X.cycles n f g) := by
  rw [IsZero.iff_id_eq_zero, ‚Üê cancel_mono (X.iCycles ..)]
  apply h.eq_of_tgt

end

section

variable (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ) {i j k : Œπ} (f : i ‚ü∂ j) (g : j ‚ü∂ k)

@[reassoc (attr := simp)]
lemma iCycles_Œ¥ : X.iCycles n‚ÇÄ f g ‚â´ X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g = 0 := by
  subst hn‚ÇÅ
  simp [iCycles]

@[reassoc (attr := simp)]
lemma Œ¥_pOpcycles : X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚â´ X.pOpcycles n‚ÇÅ f g = 0 := by
  obtain rfl : n‚ÇÄ = n‚ÇÅ - 1 := by lia
  simp [pOpcycles]

/-- The short complex which expresses `X.cycles` as the kernel of `X.Œ¥`. -/
@[simps]
noncomputable def kernelSequenceCycles : ShortComplex C :=
  ShortComplex.mk _ _ (X.iCycles_Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g)

/-- The short complex which expresses `X.opcycles` as the cokernel of `X.Œ¥`. -/
@[simps]
noncomputable def cokernelSequenceOpcycles : ShortComplex C :=
  ShortComplex.mk _ _ (X.Œ¥_pOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g)

instance : Mono (X.kernelSequenceCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g).f := by
  dsimp
  infer_instance

instance : Epi (X.cokernelSequenceOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g).g := by
  dsimp
  infer_instance

lemma kernelSequenceCycles_exact :
    (X.kernelSequenceCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g).Exact := by
  subst hn‚ÇÅ
  exact ShortComplex.kernelSequence_exact _

lemma cokernelSequenceOpcycles_exact :
    (X.cokernelSequenceOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g).Exact := by
  obtain rfl : n‚ÇÄ = n‚ÇÅ - 1 := by lia
  exact ShortComplex.cokernelSequence_exact _

section

variable {A : C} (x : A ‚ü∂ (X.H n‚ÇÄ).obj (mk‚ÇÅ g)) (hx : x ‚â´ X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g = 0)

/-- Constructor for morphisms to `X.cycles`. -/
noncomputable def liftCycles :
    A ‚ü∂ X.cycles n‚ÇÄ f g :=
  kernel.lift _ x (by subst hn‚ÇÅ; exact hx)

@[reassoc (attr := simp)]
lemma liftCycles_i : X.liftCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g x hx ‚â´ X.iCycles n‚ÇÄ f g = x := by
  apply kernel.lift_Œπ

end

section

variable {A : C} (x : (X.H n‚ÇÅ).obj (mk‚ÇÅ f) ‚ü∂ A) (hx : X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚â´ x = 0)

/-- Constructor for morphisms from `X.opcycles`. -/
noncomputable def descOpcycles :
    X.opcycles n‚ÇÅ f g ‚ü∂ A :=
  cokernel.desc _ x (by
    obtain rfl : n‚ÇÄ = n‚ÇÅ -1 := by lia
    exact hx)

@[reassoc (attr := simp)]
lemma p_descOpcycles : X.pOpcycles n‚ÇÅ f g ‚â´ X.descOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g x hx = x := by
  apply cokernel.œÄ_desc

end

end

section

variable (n : ‚Ñ§) {i j k : Œπ} (f : i ‚ü∂ j) (g : j ‚ü∂ k)
  {i' j' k' : Œπ} (f' : i' ‚ü∂ j') (g' : j' ‚ü∂ k')
  {i'' j'' k'' : Œπ} (f'' : i'' ‚ü∂ j'') (g'' : j'' ‚ü∂ k'')

/-- The functoriality of `X.cycles` with respect to morphisms in
`ComposableArrows Œπ 2`. -/
noncomputable def cyclesMap (Œ± : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f' g') :
    X.cycles n f g ‚ü∂ X.cycles n f' g' :=
  X.liftCycles _ _ rfl _ _
    (X.iCycles n f g ‚â´ (X.H n).map (homMk‚ÇÅ (Œ±.app 1) (Œ±.app 2)
      (naturality' Œ± 1 2))) (by
      rw [Category.assoc, X.Œ¥_naturality n _ rfl f g f' g'
        (homMk‚ÇÅ (Œ±.app 0) (Œ±.app 1) (naturality' Œ± 0 1))
          (homMk‚ÇÅ (Œ±.app 1) (Œ±.app 2) (naturality' Œ± 1 2)) rfl,
        iCycles_Œ¥_assoc, zero_comp])

@[reassoc]
lemma cyclesMap_i (Œ± : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f' g') (Œ≤ : mk‚ÇÅ g ‚ü∂ mk‚ÇÅ g')
    (hŒ≤ : Œ≤ = homMk‚ÇÅ (Œ±.app 1) (Œ±.app 2) (naturality' Œ± 1 2)) :
    X.cyclesMap n f g f' g' Œ± ‚â´ X.iCycles n f' g' =
      X.iCycles n f g ‚â´ (X.H n).map Œ≤ := by
  subst hŒ≤
  simp [cyclesMap]

@[simp]
lemma cyclesMap_id :
    X.cyclesMap n f g f g (ùüô _) = ùüô _ := by
  rw [‚Üê cancel_mono (X.iCycles n f g),
    X.cyclesMap_i n f g f g (ùüô _) (ùüô _) (by cat_disch),
    Functor.map_id, Category.comp_id, Category.id_comp]

@[reassoc]
lemma cyclesMap_comp (Œ± : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f' g') (Œ±' : mk‚ÇÇ f' g' ‚ü∂ mk‚ÇÇ f'' g'')
    (Œ±'' : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f'' g'') (h : Œ± ‚â´ Œ±' = Œ±'') :
    X.cyclesMap n f g f' g' Œ± ‚â´ X.cyclesMap n f' g' f'' g'' Œ±' =
      X.cyclesMap n f g f'' g'' Œ±'' := by
  subst h
  rw [‚Üê cancel_mono (X.iCycles n f'' g''), Category.assoc,
    X.cyclesMap_i n f' g' f'' g'' Œ±' _ rfl,
    X.cyclesMap_i_assoc n f g f' g' Œ± _ rfl,
    ‚Üê Functor.map_comp]
  symm
  apply X.cyclesMap_i
  cat_disch

/-- The functoriality of `X.opcycles` with respect to morphisms in
`ComposableArrows Œπ 2`. -/
noncomputable def opcyclesMap (Œ± : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f' g') :
    X.opcycles n f g ‚ü∂ X.opcycles n f' g' :=
  X.descOpcycles (n - 1) n (by lia) _ _
    ((X.H n).map (homMk‚ÇÅ (by exact Œ±.app 0) (by exact Œ±.app 1)
      (naturality' Œ± 0 1)) ‚â´ X.pOpcycles n f' g') (by
        rw [‚Üê X.Œ¥_naturality_assoc (n - 1) n (by lia) f g f' g'
          (homMk‚ÇÅ (Œ±.app 0) (Œ±.app 1) (naturality' Œ± 0 1))
          (homMk‚ÇÅ (Œ±.app 1) (Œ±.app 2) (naturality' Œ± 1 2)) rfl,
          Œ¥_pOpcycles, comp_zero])

@[reassoc]
lemma p_opcyclesMap (Œ± : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f' g') (Œ≤ : mk‚ÇÅ f ‚ü∂ mk‚ÇÅ f')
    (hŒ≤ : Œ≤ = homMk‚ÇÅ (Œ±.app 0) (Œ±.app 1) (naturality' Œ± 0 1)) :
    X.pOpcycles n f g ‚â´ X.opcyclesMap n f g f' g' Œ± =
      (X.H n).map Œ≤ ‚â´ X.pOpcycles n f' g' := by
  subst hŒ≤
  simp [opcyclesMap]

@[simp]
lemma opcyclesMap_id :
    X.opcyclesMap n f g f g (ùüô _) = ùüô _ := by
  rw [‚Üê cancel_epi (X.pOpcycles n f g),
    X.p_opcyclesMap n f g f g (ùüô _) (ùüô _) (by cat_disch),
    Functor.map_id, Category.comp_id, Category.id_comp]

lemma opcyclesMap_comp (Œ± : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f' g') (Œ±' : mk‚ÇÇ f' g' ‚ü∂ mk‚ÇÇ f'' g'')
    (Œ±'' : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f'' g'') (h : Œ± ‚â´ Œ±' = Œ±'') :
    X.opcyclesMap n f g f' g' Œ± ‚â´ X.opcyclesMap n f' g' f'' g'' Œ±' =
      X.opcyclesMap n f g f'' g'' Œ±'' := by
  subst h
  rw [‚Üê cancel_epi (X.pOpcycles n f g),
    X.p_opcyclesMap_assoc n f g f' g' Œ± _ rfl,
    X.p_opcyclesMap n f' g' f'' g'' Œ±' _ rfl,
    ‚Üê Functor.map_comp_assoc]
  symm
  apply X.p_opcyclesMap
  aesop_cat

variable (fg : i ‚ü∂ k) (h : f ‚â´ g = fg) (fg' : i' ‚ü∂ k') (h' : f' ‚â´ g' = fg')

/-- `X.cycles` also identifies to a cokernel. More precisely,
`Z^n(f, g)` identifies to the cokernel of `H^n(f) ‚ü∂ H^n(f ‚â´ g)` -/
noncomputable def cokernelIsoCycles :
    cokernel ((X.H n).map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f g fg h)) ‚âÖ X.cycles n f g :=
  (X.composableArrows‚ÇÖ_exact n _ rfl f g fg h).cokerIsoKer 0

@[reassoc (attr := simp)]
lemma cokernelIsoCycles_hom_fac :
    cokernel.œÄ _ ‚â´ (X.cokernelIsoCycles n f g fg h).hom ‚â´
      X.iCycles n f g = (X.H n).map (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f g fg h) :=
  (X.composableArrows‚ÇÖ_exact n _ rfl f g fg h).cokerIsoKer_hom_fac 0

/-- `X.opcycles` also identifies to a kernel. More precisely,
`opZ(f, g)` identifies to the kernel of `H^n(f ‚â´ g) ‚ü∂ H^n(g)` -/
noncomputable def opcyclesIsoKernel :
    X.opcycles n f g ‚âÖ kernel ((X.H n).map (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f g fg h)) :=
  (X.composableArrows‚ÇÖ_exact (n - 1) n (by lia) f g fg h).cokerIsoKer 2

@[reassoc (attr := simp)]
lemma opcyclesIsoKernel_hom_fac :
    X.pOpcycles n f g ‚â´ (X.opcyclesIsoKernel n f g fg h).hom ‚â´
      kernel.Œπ _ = (X.H n).map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f g fg h) :=
  (X.composableArrows‚ÇÖ_exact (n - 1) n (by lia) f g fg h).cokerIsoKer_hom_fac 2

/-- The map `H^n(fg) ‚ü∂ H^n(g)` factors through `Z^n(f, g)`. -/
noncomputable def toCycles : (X.H n).obj (mk‚ÇÅ fg) ‚ü∂ X.cycles n f g :=
  kernel.lift _ ((X.H n).map (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f g fg h)) (by simp)

instance : Epi (X.toCycles n f g fg h) :=
  (ShortComplex.exact_iff_epi_kernel_lift _).1 (X.exact‚ÇÉ n _ rfl f g fg h)

@[reassoc (attr := simp)]
lemma toCycles_i :
    X.toCycles n f g fg h ‚â´ X.iCycles n f g = (X.H n).map (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f g fg h) :=
  kernel.lift_Œπ ..

@[reassoc]
lemma toCycles_cyclesMap (Œ± : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f' g') (Œ≤ : mk‚ÇÅ fg ‚ü∂ mk‚ÇÅ fg')
    (hŒ≤‚ÇÄ : Œ≤.app 0 = Œ±.app 0) (hŒ≤‚ÇÅ : Œ≤.app 1 = Œ±.app 2) :
    X.toCycles n f g fg h ‚â´ X.cyclesMap n f g f' g' Œ± =
      (X.H n).map Œ≤ ‚â´ X.toCycles n f' g' fg' h' := by
  rw [‚Üê cancel_mono (X.iCycles n f' g'), Category.assoc, Category.assoc, toCycles_i,
    X.cyclesMap_i n f g f' g' Œ± (homMk‚ÇÅ (Œ±.app 1) (Œ±.app 2) (naturality' Œ± 1 2)) rfl,
    toCycles_i_assoc, ‚Üê Functor.map_comp, ‚Üê Functor.map_comp]
  congr 1
  ext
  ¬∑ dsimp
    rw [hŒ≤‚ÇÄ]
    exact naturality' Œ± 0 1
  ¬∑ dsimp
    rw [hŒ≤‚ÇÅ, Category.comp_id, Category.id_comp]

/-- The map `H^n(f) ‚ü∂ H^n(f ‚â´ g)` factors through `opZ^n(f, g)`. -/
noncomputable def fromOpcycles :
    X.opcycles n f g ‚ü∂ (X.H n).obj (mk‚ÇÅ fg) :=
  cokernel.desc _ ((X.H n).map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f g fg h)) (by simp)

instance : Mono (X.fromOpcycles n f g fg h) :=
  (ShortComplex.exact_iff_mono_cokernel_desc _).1 (X.exact‚ÇÅ (n - 1) n (by lia) f g fg h)

@[reassoc (attr := simp)]
lemma p_fromOpcycles :
    X.pOpcycles n f g ‚â´ X.fromOpcycles n f g fg h =
      (X.H n).map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f g fg h) :=
  cokernel.œÄ_desc ..

@[reassoc]
lemma opcyclesMap_fromOpcycles (Œ± : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f' g') (Œ≤ : mk‚ÇÅ fg ‚ü∂ mk‚ÇÅ fg')
    (hŒ≤‚ÇÄ : Œ≤.app 0 = Œ±.app 0) (hŒ≤‚ÇÅ : Œ≤.app 1 = Œ±.app 2) :
    X.opcyclesMap n f g f' g' Œ± ‚â´ X.fromOpcycles n f' g' fg' h' =
      X.fromOpcycles n f g fg h ‚â´ (X.H n).map Œ≤ := by
  rw [‚Üê cancel_epi (X.pOpcycles n f g), p_fromOpcycles_assoc,
    X.p_opcyclesMap_assoc n f g f' g' Œ± (homMk‚ÇÅ (Œ±.app 0) (Œ±.app 1)
      (naturality' Œ± 0 1)) rfl,
    p_fromOpcycles, ‚Üê Functor.map_comp, ‚Üê Functor.map_comp]
  congr 1
  ext
  ¬∑ cat_disch
  ¬∑ dsimp
    rw [hŒ≤‚ÇÅ]
    exact (naturality' Œ± 1 2).symm

@[reassoc (attr := simp)]
lemma H_map_twoŒ¥‚ÇÇToŒ¥‚ÇÅ_toCycles :
    (X.H n).map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f g fg h) ‚â´ X.toCycles n f g fg h = 0 := by
  simp [‚Üê cancel_mono (X.iCycles n f g)]

@[reassoc (attr := simp)]
lemma fromOpcycles_H_map_twoŒ¥‚ÇÅToŒ¥‚ÇÄ :
    X.fromOpcycles n f g fg h ‚â´ (X.H n).map (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f g fg h) = 0 := by
  simp [‚Üê cancel_epi (X.pOpcycles n f g)]

/-- The short complex expressing `Z^n(f, g)` as a cokernel of
the map `H^n(f) ‚ü∂ H^n(f ‚â´ g)`. -/
@[simps]
noncomputable def cokernelSequenceCycles : ShortComplex C :=
  ShortComplex.mk _ _ (X.H_map_twoŒ¥‚ÇÇToŒ¥‚ÇÅ_toCycles n f g fg h)

/-- The short complex expressing `opZ^n(f, g)` as a kernel of
the map `H^n(f ‚â´ g) ‚ü∂ H^n(g)`. -/
@[simps]
noncomputable def kernelSequenceOpcycles : ShortComplex C :=
  ShortComplex.mk _ _ (X.fromOpcycles_H_map_twoŒ¥‚ÇÅToŒ¥‚ÇÄ n f g fg h)

instance : Epi (X.cokernelSequenceCycles n f g fg h).g := by
  dsimp
  infer_instance

instance : Mono (X.kernelSequenceOpcycles n f g fg h).f := by
  dsimp
  infer_instance

/-- `Z^n(f, g)` identifies to a cokernel of the `H^n(f) ‚ü∂ H^n(f ‚â´ g)`. -/
lemma cokernelSequenceCycles_exact :
    (X.cokernelSequenceCycles n f g fg h).Exact := by
  apply ShortComplex.exact_of_g_is_cokernel
  exact IsColimit.ofIsoColimit (cokernelIsCokernel _)
    (Cofork.ext (X.cokernelIsoCycles n f g fg h) (by
      simp [‚Üê cancel_mono (X.iCycles n f g)]))

/-- `opZ^n(f, g)` identifies to the kernel of `H^n(f ‚â´ g) ‚ü∂ H^n(g)`. -/
lemma kernelSequenceOpcycles_exact :
    (X.kernelSequenceOpcycles n f g fg h).Exact := by
  apply ShortComplex.exact_of_f_is_kernel
  exact IsLimit.ofIsoLimit (kernelIsKernel _)
    (Iso.symm (Fork.ext (X.opcyclesIsoKernel n f g fg h) (by
      simp [‚Üê cancel_epi (X.pOpcycles n f g)])))

lemma isIso_toCycles (hf : IsZero ((X.H n).obj (mk‚ÇÅ f))) :
    IsIso (X.toCycles n f g fg h) := by
  have : Mono (X.toCycles n f g fg h) :=
    (X.cokernelSequenceCycles_exact n f g fg h).mono_g (hf.eq_of_src _ _)
  exact Balanced.isIso_of_mono_of_epi _

lemma isIso_fromOpcycles (hg : IsZero ((X.H n).obj (mk‚ÇÅ g))) :
    IsIso (X.fromOpcycles n f g fg h) := by
  have : Epi (X.fromOpcycles n f g fg h) :=
    (X.kernelSequenceOpcycles_exact n f g fg h).epi_f (hg.eq_of_tgt _ _)
  exact Balanced.isIso_of_mono_of_epi _

section

variable {A : C} (x : (X.H n).obj (mk‚ÇÅ fg) ‚ü∂ A)
  (hx : (X.H n).map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f g fg h) ‚â´ x = 0)

/-- Constructor for morphisms from `X.cycles`. -/
noncomputable def descCycles :
    X.cycles n f g ‚ü∂ A :=
  (X.cokernelSequenceCycles_exact n f g fg h).desc x hx

@[reassoc (attr := simp)]
lemma toCycles_descCycles :
    X.toCycles n f g fg h ‚â´ X.descCycles n f g fg h x hx = x :=
  (X.cokernelSequenceCycles_exact n f g fg h).g_desc x hx

end

section

variable {A : C} (x : A ‚ü∂ (X.H n).obj (mk‚ÇÅ fg))
  (hx : x ‚â´ (X.H n).map (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f g fg h) = 0)

/-- Constructor for morphisms to `X.opcycles`. -/
noncomputable def liftOpcycles :
    A ‚ü∂ X.opcycles n f g :=
  (X.kernelSequenceOpcycles_exact n f g fg h).lift x hx

@[reassoc (attr := simp)]
lemma liftOpcycles_fromOpcycles :
    X.liftOpcycles n f g fg h x hx ‚â´ X.fromOpcycles n f g fg h = x :=
  (X.kernelSequenceOpcycles_exact n f g fg h).lift_f x hx

end

end

section

variable (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ)
  {i j k l : Œπ} (f‚ÇÅ : i ‚ü∂ j) (f‚ÇÇ : j ‚ü∂ k) (f‚ÇÉ : k ‚ü∂ l)
  (f‚ÇÅ‚ÇÇ : i ‚ü∂ k) (h‚ÇÅ‚ÇÇ : f‚ÇÅ ‚â´ f‚ÇÇ = f‚ÇÅ‚ÇÇ) (f‚ÇÇ‚ÇÉ : j ‚ü∂ l) (h‚ÇÇ‚ÇÉ : f‚ÇÇ ‚â´ f‚ÇÉ = f‚ÇÇ‚ÇÉ)

noncomputable def Œ¥ToCycles : (X.H n‚ÇÄ).obj (mk‚ÇÅ f‚ÇÉ) ‚ü∂ X.cycles n‚ÇÅ f‚ÇÅ f‚ÇÇ :=
  X.liftCycles n‚ÇÅ _ rfl f‚ÇÅ f‚ÇÇ (X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÇ f‚ÇÉ) (by simp)

@[reassoc (attr := simp)]
lemma Œ¥ToCycles_iCycles :
    X.Œ¥ToCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚â´ X.iCycles n‚ÇÅ f‚ÇÅ f‚ÇÇ =
      X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÇ f‚ÇÉ := by
  simp only [Œ¥ToCycles, liftCycles_i]

@[reassoc (attr := simp)]
lemma Œ¥_toCycles :
    X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÉ ‚â´ X.toCycles n‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ =
      X.Œ¥ToCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÉ := by
  rw [‚Üê cancel_mono (X.iCycles n‚ÇÅ f‚ÇÅ f‚ÇÇ), Category.assoc,
    toCycles_i, Œ¥ToCycles_iCycles,
    ‚Üê X.Œ¥_naturality n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÉ f‚ÇÇ f‚ÇÉ (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f‚ÇÅ f‚ÇÇ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ) (ùüô _) rfl,
    Functor.map_id, Category.id_comp]

noncomputable def Œ¥FromOpcycles : X.opcycles n‚ÇÄ f‚ÇÇ f‚ÇÉ ‚ü∂ (X.H n‚ÇÅ).obj (mk‚ÇÅ f‚ÇÅ) :=
  X.descOpcycles (n‚ÇÄ - 1) n‚ÇÄ (by lia) f‚ÇÇ f‚ÇÉ (X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÅ f‚ÇÇ) (by simp)

@[reassoc (attr := simp)]
lemma pOpcycles_Œ¥FromOpcycles :
    X.pOpcycles n‚ÇÄ f‚ÇÇ f‚ÇÉ ‚â´ X.Œ¥FromOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÉ =
      X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÅ f‚ÇÇ := by
  simp only [Œ¥FromOpcycles, p_descOpcycles]

@[reassoc (attr := simp)]
lemma fromOpcyles_Œ¥ :
    X.fromOpcycles n‚ÇÄ f‚ÇÇ f‚ÇÉ f‚ÇÇ‚ÇÉ h‚ÇÇ‚ÇÉ ‚â´ X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÅ f‚ÇÇ‚ÇÉ =
      X.Œ¥FromOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÉ := by
  rw [‚Üê cancel_epi (X.pOpcycles n‚ÇÄ f‚ÇÇ f‚ÇÉ),
    p_fromOpcycles_assoc, pOpcycles_Œ¥FromOpcycles,
    X.Œ¥_naturality n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÅ f‚ÇÇ‚ÇÉ (ùüô _) (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÇ‚ÇÉ h‚ÇÇ‚ÇÉ) rfl,
    Functor.map_id, Category.comp_id]

end

end SpectralObject

end Abelian

end CategoryTheory
