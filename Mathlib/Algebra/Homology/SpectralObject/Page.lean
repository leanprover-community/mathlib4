/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.Algebra.Homology.SpectralObject.Basic
public import Mathlib.Algebra.Homology.ExactSequenceFour

/-!
# Spectral objects in abelian categories

Let `X` be a spectral object index by the category `Œπ`
in the abelian category `C`. The purpose of this file
is two introduce the homology `X.E` of the short complex `X.shortComplexE`
`(X.H n‚ÇÄ).obj (mk‚ÇÅ f‚ÇÉ) ‚ü∂ (X.H n‚ÇÅ).obj (mk‚ÇÅ f‚ÇÇ) ‚ü∂ (X.H n‚ÇÇ).obj (mk‚ÇÅ f‚ÇÅ)`
when `f‚ÇÅ`, `f‚ÇÇ` and `f‚ÇÉ` are composable morphisms in `Œπ` and the
equalities `n‚ÇÄ + 1 = n‚ÇÅ` and `n‚ÇÅ + 1 = n‚ÇÇ` hold (both maps in the
short complex are given by `X.Œ¥`). All the relevant objects in the
spectral sequence attached to spectral objects can be defined
in terms of this homology `X.E`: the objects in all pages, including
the page at infinity.

In order to study this homology, we introduce objects `X.cycles`
for the kernel of `Œ¥` and `X.opcycles` for its cokernel. We record
the obvious exact sequences that are part of this definition
as the lemmas `kernelSequenceCycles_exact`
and `cokernelSequenceOpcycles_exact`, and constructor for morphisms
`X.liftCycles` to cycles and `X.descOpcycles` from opcycles.
The definitions `cyclesMap` and `opcyclesMap` give the functoriality
with respect to `ComposableArrows Œπ 2`.

The fact that the morphisms `Œ¥` are part of a long exact sequence allow
to show that `X.cycles` also identify to a cokernel (`cokernelIsoCycles`)
and `X.opcycles` to a kernel (`opcyclesIsoKernel`). In particular, we also
get constructors `descCycles` and `liftOpcycles` for morphisms from cycles
and to opcycles.

## References
* [Jean-Louis Verdier, *Des cat√©gories d√©riv√©es des cat√©gories ab√©liennes*, II.4][verdier1996]

-/

@[expose] public section

namespace CategoryTheory

open Limits ComposableArrows

namespace Abelian

variable {C Œπ : Type*} [Category C] [Category Œπ] [Abelian C]

namespace SpectralObject
variable (X : SpectralObject C Œπ)

section

variable (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ)
  {i j k : Œπ} (f : i ‚ü∂ j) (g : j ‚ü∂ k)
  {i' j' k' : Œπ} (f' : i' ‚ü∂ j') (g' : j' ‚ü∂ k')
  {i'' j'' k'' : Œπ} (f'' : i'' ‚ü∂ j'') (g'' : j'' ‚ü∂ k'')

/-- The kernel of `Œ¥`. -/
noncomputable def cycles : C := kernel (X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g)

/-- The cokernel of `Œ¥`. -/
noncomputable def opcycles : C := cokernel (X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g)

/-- The inclusion `X.cycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚ü∂ (X.H n‚ÇÄ).obj (mk‚ÇÅ g)`
of the kernel of `Œ¥`. -/
noncomputable def iCycles :
    X.cycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚ü∂ (X.H n‚ÇÄ).obj (mk‚ÇÅ g) :=
  kernel.Œπ _

/-- The projection `(X.H n‚ÇÅ).obj (mk‚ÇÅ f) ‚ü∂ X.opcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g`
to the cokernel of `Œ¥`. -/
noncomputable def pOpcycles :
    (X.H n‚ÇÅ).obj (mk‚ÇÅ f) ‚ü∂ X.opcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g :=
  cokernel.œÄ _

instance : Mono (X.iCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g) := by
  dsimp [iCycles]
  infer_instance

instance : Epi (X.pOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g) := by
  dsimp [pOpcycles]
  infer_instance

@[reassoc (attr := simp)]
lemma iCycles_Œ¥ : X.iCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚â´ X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g = 0 := by
  simp [iCycles]

@[reassoc (attr := simp)]
lemma Œ¥_pOpcycles : X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚â´ X.pOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g = 0 := by
  simp [pOpcycles]

/-- The short complex which expresses `X.cycles` as the kernel of `X.Œ¥`. -/
@[simps]
noncomputable def kernelSequenceCycles : ShortComplex C :=
  ShortComplex.mk _ _ (X.iCycles_Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g)

/-- The short complex which expresses `X.opcycles` as the cokernel of `X.Œ¥`. -/
@[simps]
noncomputable def cokernelSequenceOpcycles : ShortComplex C :=
  ShortComplex.mk _ _ (X.Œ¥_pOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g)

instance : Mono (X.kernelSequenceCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g).f := by
  dsimp
  infer_instance

instance : Epi (X.cokernelSequenceOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g).g := by
  dsimp
  infer_instance

lemma kernelSequenceCycles_exact :
    (X.kernelSequenceCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g).Exact :=
  ShortComplex.kernelSequence_exact _

lemma cokernelSequenceOpcycles_exact :
    (X.cokernelSequenceOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g).Exact :=
  ShortComplex.cokernelSequence_exact _

section

variable {A : C} (x : A ‚ü∂ (X.H n‚ÇÄ).obj (mk‚ÇÅ g)) (hx : x ‚â´ X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g = 0)

/-- Constructor for morphisms to `X.cycles`. -/
noncomputable def liftCycles :
    A ‚ü∂ X.cycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g :=
  kernel.lift _ x hx

@[reassoc (attr := simp)]
lemma liftCycles_i : X.liftCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g x hx ‚â´ X.iCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g = x := by
  apply kernel.lift_Œπ

end

section

variable {A : C} (x : (X.H n‚ÇÅ).obj (mk‚ÇÅ f) ‚ü∂ A) (hx : X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚â´ x = 0)

/-- Constructor for morphisms from `X.opcycles`. -/
noncomputable def descOpcycles :
    X.opcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚ü∂ A :=
  cokernel.desc _ x hx

@[reassoc (attr := simp)]
lemma p_descOpcycles : X.pOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚â´ X.descOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g x hx = x := by
  apply cokernel.œÄ_desc

end

/-- The functoriality of `X.cycles` with respect to morphisms in
`ComposableArrows Œπ 2`. -/
noncomputable def cyclesMap (Œ± : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f' g') :
    X.cycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚ü∂ X.cycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f' g' :=
  kernel.lift _ (X.iCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚â´
      (X.H n‚ÇÄ).map (homMk‚ÇÅ (Œ±.app 1) (Œ±.app 2) (naturality' Œ± 1 2))) (by
      rw [Category.assoc, X.Œ¥_naturality n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g f' g'
        (homMk‚ÇÅ (Œ±.app 0) (Œ±.app 1) (naturality' Œ± 0 1))
          (homMk‚ÇÅ (Œ±.app 1) (Œ±.app 2) (naturality' Œ± 1 2)) rfl, iCycles_Œ¥_assoc, zero_comp])

@[reassoc]
lemma cyclesMap_i (Œ± : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f' g') (Œ≤ : mk‚ÇÅ g ‚ü∂ mk‚ÇÅ g')
    (hŒ≤ : Œ≤ = homMk‚ÇÅ (Œ±.app 1) (Œ±.app 2) (naturality' Œ± 1 2)) :
    X.cyclesMap n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g f' g' Œ± ‚â´ X.iCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f' g' =
      X.iCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚â´ (X.H n‚ÇÄ).map Œ≤ := by
  subst hŒ≤
  apply kernel.lift_Œπ

@[simp]
lemma cyclesMap_id :
    X.cyclesMap n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g f g (ùüô _) = ùüô _ := by
  rw [‚Üê cancel_mono (X.iCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g),
    X.cyclesMap_i n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g f g (ùüô _) (ùüô _) (by aesop_cat),
    Functor.map_id, Category.comp_id, Category.id_comp]

lemma cyclesMap_comp (Œ± : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f' g') (Œ±' : mk‚ÇÇ f' g' ‚ü∂ mk‚ÇÇ f'' g'')
    (Œ±'' : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f'' g'') (h : Œ± ‚â´ Œ±' = Œ±'') :
    X.cyclesMap n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g f' g' Œ± ‚â´ X.cyclesMap n‚ÇÄ n‚ÇÅ hn‚ÇÅ f' g' f'' g'' Œ±' =
      X.cyclesMap n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g f'' g'' Œ±'' := by
  subst h
  rw [‚Üê cancel_mono (X.iCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f'' g''), Category.assoc,
    X.cyclesMap_i n‚ÇÄ n‚ÇÅ hn‚ÇÅ f' g' f'' g'' Œ±' _ rfl,
    X.cyclesMap_i_assoc n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g f' g' Œ± _ rfl,
    ‚Üê Functor.map_comp]
  symm
  apply X.cyclesMap_i
  aesop_cat

/-- The functoriality of `X.opcycles` with respect to morphisms in
`ComposableArrows Œπ 2`. -/
noncomputable def opcyclesMap (Œ± : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f' g') :
    X.opcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚ü∂ X.opcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f' g' :=
  cokernel.desc _
    ((X.H n‚ÇÅ).map (homMk‚ÇÅ (by exact Œ±.app 0) (by exact Œ±.app 1) (by exact naturality' Œ± 0 1)) ‚â´
      X.pOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f' g') (by
        rw [‚Üê X.Œ¥_naturality_assoc n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g f' g'
          (homMk‚ÇÅ (Œ±.app 0) (Œ±.app 1) (naturality' Œ± 0 1))
          (homMk‚ÇÅ (Œ±.app 1) (Œ±.app 2) (naturality' Œ± 1 2)) rfl, Œ¥_pOpcycles, comp_zero])

@[reassoc]
lemma p_opcyclesMap (Œ± : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f' g') (Œ≤ : mk‚ÇÅ f ‚ü∂ mk‚ÇÅ f')
    (hŒ≤ : Œ≤ = homMk‚ÇÅ (Œ±.app 0) (Œ±.app 1) (naturality' Œ± 0 1)) :
    X.pOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚â´ X.opcyclesMap n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g f' g' Œ± =
      (X.H n‚ÇÅ).map Œ≤ ‚â´ X.pOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f' g' := by
  subst hŒ≤
  apply cokernel.œÄ_desc

@[simp]
lemma opcyclesMap_id :
    X.opcyclesMap n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g f g (ùüô _) = ùüô _ := by
  rw [‚Üê cancel_epi (X.pOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g),
    X.p_opcyclesMap n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g f g (ùüô _) (ùüô _) (by aesop_cat),
    Functor.map_id, Category.comp_id, Category.id_comp]

lemma opcyclesMap_comp (Œ± : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f' g') (Œ±' : mk‚ÇÇ f' g' ‚ü∂ mk‚ÇÇ f'' g'')
    (Œ±'' : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f'' g'') (h : Œ± ‚â´ Œ±' = Œ±'') :
    X.opcyclesMap n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g f' g' Œ± ‚â´ X.opcyclesMap n‚ÇÄ n‚ÇÅ hn‚ÇÅ f' g' f'' g'' Œ±' =
      X.opcyclesMap n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g f'' g'' Œ±'' := by
  subst h
  rw [‚Üê cancel_epi (X.pOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g),
    X.p_opcyclesMap_assoc n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g f' g' Œ± _ rfl,
    X.p_opcyclesMap n‚ÇÄ n‚ÇÅ hn‚ÇÅ f' g' f'' g'' Œ±' _ rfl,
    ‚Üê Functor.map_comp_assoc]
  symm
  apply X.p_opcyclesMap
  aesop_cat

variable (fg : i ‚ü∂ k) (h : f ‚â´ g = fg) (fg' : i' ‚ü∂ k') (h' : f' ‚â´ g' = fg')

/-- `X.cycles` also identifies to a cokernel. -/
noncomputable def cokernelIsoCycles :
    cokernel ((X.H n‚ÇÄ).map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f g fg h)) ‚âÖ X.cycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g :=
  (X.composableArrows‚ÇÖ_exact n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h).cokerIsoKer 0

@[reassoc (attr := simp)]
lemma cokernelIsoCycles_hom_fac :
    cokernel.œÄ _ ‚â´ (X.cokernelIsoCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h).hom ‚â´
      X.iCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g = (X.H n‚ÇÄ).map (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f g fg h) :=
  (X.composableArrows‚ÇÖ_exact n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h).cokerIsoKer_hom_fac 0

/-- `X.opcycles` also identifies to a kernel. -/
noncomputable def opcyclesIsoKernel :
    X.opcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚âÖ kernel ((X.H n‚ÇÅ).map (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f g fg h)) :=
  (X.composableArrows‚ÇÖ_exact n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h).cokerIsoKer 2

@[reassoc (attr := simp)]
lemma opcyclesIsoKernel_hom_fac :
    X.pOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚â´ (X.opcyclesIsoKernel n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h).hom ‚â´
      kernel.Œπ _ = (X.H n‚ÇÅ).map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f g fg h) :=
  (X.composableArrows‚ÇÖ_exact n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h).cokerIsoKer_hom_fac 2

/-- The map `(X.H n‚ÇÄ).obj (mk‚ÇÅ fg) ‚ü∂ (X.H n‚ÇÄ).obj (mk‚ÇÅ g)` factors through
`X.cycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g`. -/
noncomputable def toCycles : (X.H n‚ÇÄ).obj (mk‚ÇÅ fg) ‚ü∂ X.cycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g :=
  kernel.lift _ ((X.H n‚ÇÄ).map (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f g fg h)) (by simp)

instance : Epi (X.toCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h) :=
  (ShortComplex.exact_iff_epi_kernel_lift _).1 (X.exact‚ÇÉ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h)

@[reassoc (attr := simp)]
lemma toCycles_i :
    X.toCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h ‚â´ X.iCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g =
      (X.H n‚ÇÄ).map (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f g fg h) :=
  kernel.lift_Œπ ..

@[reassoc]
lemma toCycles_cyclesMap (Œ± : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f' g') (Œ≤ : mk‚ÇÅ fg ‚ü∂ mk‚ÇÅ fg')
    (hŒ≤‚ÇÄ : Œ≤.app 0 = Œ±.app 0) (hŒ≤‚ÇÅ : Œ≤.app 1 = Œ±.app 2) :
    X.toCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h ‚â´ X.cyclesMap n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g f' g' Œ± =
      (X.H n‚ÇÄ).map Œ≤ ‚â´ X.toCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f' g' fg' h' := by
  rw [‚Üê cancel_mono (X.iCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f' g'), Category.assoc, Category.assoc, toCycles_i,
    X.cyclesMap_i n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g f' g' Œ± (homMk‚ÇÅ (Œ±.app 1) (Œ±.app 2) (naturality' Œ± 1 2)) rfl,
    toCycles_i_assoc, ‚Üê Functor.map_comp, ‚Üê Functor.map_comp]
  congr 1
  ext
  ¬∑ dsimp
    rw [hŒ≤‚ÇÄ]
    exact naturality' Œ± 0 1
  ¬∑ dsimp
    rw [hŒ≤‚ÇÅ, Category.comp_id, Category.id_comp]

/-- The map `(X.H n‚ÇÅ).obj (mk‚ÇÅ f) ‚ü∂ (X.H n‚ÇÅ).obj (mk‚ÇÅ fg)` factors through
`X.opcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g`. -/
noncomputable def fromOpcycles :
    X.opcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚ü∂ (X.H n‚ÇÅ).obj (mk‚ÇÅ fg) :=
  cokernel.desc _ ((X.H n‚ÇÅ).map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f g fg h)) (by simp)

instance : Mono (X.fromOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h) :=
  (ShortComplex.exact_iff_mono_cokernel_desc _).1 (X.exact‚ÇÅ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h)

@[reassoc (attr := simp)]
lemma p_fromOpcycles :
    X.pOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚â´ X.fromOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h =
      (X.H n‚ÇÅ).map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f g fg h) :=
  cokernel.œÄ_desc ..

@[reassoc]
lemma opcyclesMap_fromOpcycles (Œ± : mk‚ÇÇ f g ‚ü∂ mk‚ÇÇ f' g') (Œ≤ : mk‚ÇÅ fg ‚ü∂ mk‚ÇÅ fg')
    (hŒ≤‚ÇÄ : Œ≤.app 0 = Œ±.app 0) (hŒ≤‚ÇÅ : Œ≤.app 1 = Œ±.app 2) :
    X.opcyclesMap n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g f' g' Œ± ‚â´ X.fromOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f' g' fg' h' =
      X.fromOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h ‚â´ (X.H n‚ÇÅ).map Œ≤ := by
  rw [‚Üê cancel_epi (X.pOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g), p_fromOpcycles_assoc,
    X.p_opcyclesMap_assoc n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g f' g' Œ± (homMk‚ÇÅ (Œ±.app 0) (Œ±.app 1)
      (naturality' Œ± 0 1)) rfl,
    p_fromOpcycles, ‚Üê Functor.map_comp, ‚Üê Functor.map_comp]
  congr 1
  ext
  ¬∑ cat_disch
  ¬∑ dsimp
    rw [hŒ≤‚ÇÅ]
    exact (naturality' Œ± 1 2).symm

@[reassoc (attr := simp)]
lemma H_map_twoŒ¥‚ÇÇToŒ¥‚ÇÅ_toCycles :
    (X.H n‚ÇÄ).map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f g fg h) ‚â´ X.toCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h = 0 := by
  simp [‚Üê cancel_mono (X.iCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g)]

@[reassoc (attr := simp)]
lemma fromOpcycles_H_map_twoŒ¥‚ÇÅToŒ¥‚ÇÄ :
    X.fromOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h ‚â´ (X.H n‚ÇÅ).map (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f g fg h) = 0 := by
  simp [‚Üê cancel_epi (X.pOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g)]

/-- The short complex expressing `X.cycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g` as a cokernel of
the map `(X.H n‚ÇÄ).obj (mk‚ÇÅ f) ‚ü∂ (X.H n‚ÇÄ).obj (mk‚ÇÅ fg)`. -/
@[simps]
noncomputable def cokernelSequenceCycles : ShortComplex C :=
  ShortComplex.mk _ _ (X.H_map_twoŒ¥‚ÇÇToŒ¥‚ÇÅ_toCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h)

/-- The short complex expressing `X.opcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g` as a kernel of
the map `(X.H n‚ÇÅ).obj (mk‚ÇÅ fg) ‚ü∂ (X.H n‚ÇÅ).obj (mk‚ÇÅ g)`. -/
@[simps]
noncomputable def kernelSequenceOpcycles : ShortComplex C :=
  ShortComplex.mk _ _ (X.fromOpcycles_H_map_twoŒ¥‚ÇÅToŒ¥‚ÇÄ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h)

instance : Epi (X.cokernelSequenceCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h).g := by
  dsimp
  infer_instance

instance : Mono (X.kernelSequenceOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h).f := by
  dsimp
  infer_instance

lemma cokernelSequenceCycles_exact :
    (X.cokernelSequenceCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h).Exact := by
  apply ShortComplex.exact_of_g_is_cokernel
  exact IsColimit.ofIsoColimit (cokernelIsCokernel _)
    (Cofork.ext (X.cokernelIsoCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h) (by
      simp [‚Üê cancel_mono (X.iCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g)]))

lemma kernelSequenceOpcycles_exact :
    (X.kernelSequenceOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h).Exact := by
  apply ShortComplex.exact_of_f_is_kernel
  exact IsLimit.ofIsoLimit (kernelIsKernel _)
    (Iso.symm (Fork.ext (X.opcyclesIsoKernel n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h) (by
      simp [‚Üê cancel_epi (X.pOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g)])))

section

variable {A : C} (x : (X.H n‚ÇÄ).obj (mk‚ÇÅ fg) ‚ü∂ A)
  (hx : (X.H n‚ÇÄ).map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f g fg h) ‚â´ x = 0)

/-- Constructor for morphisms from `X.cycles`. -/
noncomputable def descCycles :
    X.cycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g ‚ü∂ A :=
  (X.cokernelSequenceCycles_exact n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h).desc x hx

@[reassoc (attr := simp)]
lemma toCycles_descCycles :
    X.toCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h ‚â´ X.descCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h x hx = x :=
  (X.cokernelSequenceCycles_exact n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h).g_desc x hx

end

section

variable {A : C} (x : A ‚ü∂ (X.H n‚ÇÅ).obj (mk‚ÇÅ fg))
  (hx : x ‚â´ (X.H n‚ÇÅ).map (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f g fg h) = 0)

/-- Constructor for morphisms to `X.descCycles`. -/
noncomputable def liftOpcycles :
    A ‚ü∂ X.opcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g :=
  (X.kernelSequenceOpcycles_exact n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h).lift x hx

@[reassoc (attr := simp)]
lemma liftOpcycles_fromOpcycles :
    X.liftOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h x hx ‚â´ X.fromOpcycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h = x :=
  (X.kernelSequenceOpcycles_exact n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g fg h).lift_f x hx

end

end

section

variable (n‚ÇÄ n‚ÇÅ n‚ÇÇ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ) (hn‚ÇÇ : n‚ÇÅ + 1 = n‚ÇÇ)
  {i j k l : Œπ} (f‚ÇÅ : i ‚ü∂ j) (f‚ÇÇ : j ‚ü∂ k) (f‚ÇÉ : k ‚ü∂ l)

/-- The short complex consisting of the composition of
two morphisms `X.Œ¥`, given three composable morphisms `f‚ÇÅ`, `f‚ÇÇ`
and `f‚ÇÉ` in `Œπ`, and three consecutive integers. -/
@[simps]
def shortComplexE : ShortComplex C where
  X‚ÇÅ := (X.H n‚ÇÄ).obj (mk‚ÇÅ f‚ÇÉ)
  X‚ÇÇ := (X.H n‚ÇÅ).obj (mk‚ÇÅ f‚ÇÇ)
  X‚ÇÉ := (X.H n‚ÇÇ).obj (mk‚ÇÅ f‚ÇÅ)
  f := X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÇ f‚ÇÉ
  g := X.Œ¥ n‚ÇÅ n‚ÇÇ hn‚ÇÇ f‚ÇÅ f‚ÇÇ
  zero := by simp

/-- The homology of the short complex `shortComplexE` consisting of
two morphisms `X.Œ¥`. -/
noncomputable def E : C := (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).homology

lemma isZero_E_of_isZero_H (h : IsZero ((X.H n‚ÇÅ).obj (mk‚ÇÅ f‚ÇÇ))) :
    IsZero (X.E n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ) :=
  (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).exact_iff_isZero_homology.1
    (ShortComplex.exact_of_isZero_X‚ÇÇ _ h)

end

section

variable (n‚ÇÄ n‚ÇÅ n‚ÇÇ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ) (hn‚ÇÇ : n‚ÇÅ + 1 = n‚ÇÇ)
  {i j k l : Œπ}
  {i j k l : Œπ} (f‚ÇÅ : i ‚ü∂ j) (f‚ÇÇ : j ‚ü∂ k) (f‚ÇÉ : k ‚ü∂ l)
  {i' j' k' l' : Œπ} (f‚ÇÅ' : i' ‚ü∂ j') (f‚ÇÇ' : j' ‚ü∂ k') (f‚ÇÉ' : k' ‚ü∂ l')
  {i'' j'' k'' l'' : Œπ} (f‚ÇÅ'' : i'' ‚ü∂ j'') (f‚ÇÇ'' : j'' ‚ü∂ k'') (f‚ÇÉ'' : k'' ‚ü∂ l'')
  (Œ± : mk‚ÇÉ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚ü∂ mk‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ')
  (Œ≤ : mk‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' ‚ü∂ mk‚ÇÉ f‚ÇÅ'' f‚ÇÇ'' f‚ÇÉ'')
  (Œ≥ : mk‚ÇÉ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚ü∂ mk‚ÇÉ f‚ÇÅ'' f‚ÇÇ'' f‚ÇÉ'')

/-- The functoriality of `shortComplexE` with respect to morphisms
in `ComposableArrows Œπ 3`. -/
@[simps]
def shortComplexEMap :
    X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚ü∂
      X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' where
  œÑ‚ÇÅ := (X.H n‚ÇÄ).map (homMk‚ÇÅ (Œ±.app 2) (Œ±.app 3) (naturality' Œ± 2 3))
  œÑ‚ÇÇ := (X.H n‚ÇÅ).map (homMk‚ÇÅ (Œ±.app 1) (Œ±.app 2) (naturality' Œ± 1 2))
  œÑ‚ÇÉ := (X.H n‚ÇÇ).map (homMk‚ÇÅ (Œ±.app 0) (Œ±.app 1) (naturality' Œ± 0 1))
  comm‚ÇÅ‚ÇÇ := Œ¥_naturality ..
  comm‚ÇÇ‚ÇÉ := Œ¥_naturality ..

@[simp]
lemma shortComplexEMap_id :
    X.shortComplexEMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ f‚ÇÇ f‚ÇÉ (ùüô _) = ùüô _ := by
  ext
  all_goals dsimp; convert (X.H _).map_id _; cat_disch

@[reassoc, simp]
lemma shortComplexEMap_comp :
    X.shortComplexEMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ'' f‚ÇÇ'' f‚ÇÉ'' (Œ± ‚â´ Œ≤) =
    X.shortComplexEMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' Œ± ‚â´
      X.shortComplexEMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' f‚ÇÅ'' f‚ÇÇ'' f‚ÇÉ'' Œ≤ := by
  ext
  all_goals dsimp; rw [‚Üê Functor.map_comp]; congr 1; cat_disch

/-- The functoriality of `E` with respect to morphisms
in `ComposableArrows Œπ 3`. -/
noncomputable def EMap :
    X.E n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚ü∂ X.E n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' :=
  ShortComplex.homologyMap (X.shortComplexEMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' Œ±)

@[simp]
lemma EMap_id :
    X.EMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ f‚ÇÇ f‚ÇÉ (ùüô _) = ùüô _ := by
  dsimp only [EMap]
  rw [shortComplexEMap_id, ShortComplex.homologyMap_id]
  rfl

/-- Variant of `EMap_id`. -/
lemma EMap_id' (Œ± : mk‚ÇÉ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚ü∂ mk‚ÇÉ f‚ÇÅ f‚ÇÇ f‚ÇÉ) (hŒ± : Œ± = ùüô _) :
    X.EMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ f‚ÇÇ f‚ÇÉ Œ± = ùüô _ := by
  subst hŒ±
  simp only [EMap_id]

@[reassoc, simp]
lemma EMap_comp :
    X.EMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ'' f‚ÇÇ'' f‚ÇÉ'' (Œ± ‚â´ Œ≤) =
    X.EMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' Œ± ‚â´
      X.EMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' f‚ÇÅ'' f‚ÇÇ'' f‚ÇÉ'' Œ≤ := by
  dsimp only [EMap]
  rw [shortComplexEMap_comp, ShortComplex.homologyMap_comp]

lemma isIso_EMap
    (h‚ÇÄ : IsIso ((X.H n‚ÇÄ).map ((functorArrows Œπ 2 3 3).map Œ±)))
    (h‚ÇÅ : IsIso ((X.H n‚ÇÅ).map ((functorArrows Œπ 1 2 3).map Œ±)))
    (h‚ÇÇ : IsIso ((X.H n‚ÇÇ).map ((functorArrows Œπ 0 1 3).map Œ±))) :
    IsIso (X.EMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' Œ±) := by
  have : IsIso (shortComplexEMap X n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' Œ±) := by
    apply (config := { allowSynthFailures := true})
      ShortComplex.isIso_of_isIso <;> assumption
  dsimp [EMap]
  infer_instance

end

section

variable (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ)
  {i j k : Œπ} (f : i ‚ü∂ j) (g : j ‚ü∂ k)

lemma Œ¥_eq_zero_of_isIso‚ÇÅ (hf : IsIso f) :
    X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g = 0 := by
  simpa only [Preadditive.IsIso.comp_left_eq_zero] using X.zero‚ÇÉ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g _ rfl

lemma Œ¥_eq_zero_of_isIso‚ÇÇ (hg : IsIso g) :
    X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g = 0 := by
  simpa only [Preadditive.IsIso.comp_right_eq_zero] using X.zero‚ÇÅ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g _ rfl

end

lemma isZero_H_obj_of_isIso (n : ‚Ñ§) {i j : Œπ} (f : i ‚ü∂ j) (hf : IsIso f) :
    IsZero ((X.H n).obj (mk‚ÇÅ f)) := by
  let e : mk‚ÇÅ (ùüô i) ‚âÖ mk‚ÇÅ f := isoMk‚ÇÅ (Iso.refl _) (asIso f) (by simp)
  refine IsZero.of_iso ?_ ((X.H n).mapIso e.symm)
  have h := X.zero‚ÇÇ n (ùüô i) (ùüô i) (ùüô i) (by simp)
  rw [‚Üê Functor.map_comp] at h
  rw [IsZero.iff_id_eq_zero, ‚Üê Functor.map_id, ‚Üê h]
  congr 1
  cat_disch

section

variable (n‚ÇÄ n‚ÇÅ n‚ÇÇ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ) (hn‚ÇÇ : n‚ÇÅ + 1 = n‚ÇÇ)
  {i j : Œπ} (f : i ‚ü∂ j) {i' j' : Œπ} (f' : i' ‚ü∂ j')

/-- An homology data for `X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ (ùüô i) f (ùüô j)`,
expressing `(X.H n‚ÇÅ).obj (mk‚ÇÅ f)` as the homology of this short complex,
see `EIsoH`. -/
@[simps!]
noncomputable def homologyDataEIdId :
    (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ (ùüô i) f (ùüô j)).HomologyData :=
  (ShortComplex.HomologyData.ofZeros (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ (ùüô i) f (ùüô j))
    (X.Œ¥_eq_zero_of_isIso‚ÇÇ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f (ùüô j) inferInstance)
    (X.Œ¥_eq_zero_of_isIso‚ÇÅ n‚ÇÅ n‚ÇÇ hn‚ÇÇ (ùüô i) f inferInstance))

/-- `(X.H n‚ÇÅ).obj (mk‚ÇÅ f)` identifies to `X.E` applied to the composable
morphisms `ùüô _, f, ùüô _`. -/
noncomputable def EIsoH :
    X.E n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ (ùüô i) f (ùüô j) ‚âÖ (X.H n‚ÇÅ).obj (mk‚ÇÅ f) :=
  (X.homologyDataEIdId ..).left.homologyIso

lemma EIsoH_hom_naturality
    (Œ± : mk‚ÇÅ f ‚ü∂ mk‚ÇÅ f') (Œ≤ : mk‚ÇÉ (ùüô _) f (ùüô _) ‚ü∂ mk‚ÇÉ (ùüô _) f' (ùüô _))
    (hŒ≤ : Œ≤ = homMk‚ÇÉ (Œ±.app 0) (Œ±.app 0) (Œ±.app 1) (Œ±.app 1)
      (by simp) (naturality' Œ± 0 1) (by simp [Precomp.obj, Precomp.map])) :
    X.EMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ (ùüô _) f (ùüô _) (ùüô _) f' (ùüô _) Œ≤ ‚â´
      (X.EIsoH n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f').hom =
    (X.EIsoH n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f).hom ‚â´ (X.H n‚ÇÅ).map Œ± := by
  obtain rfl : Œ± = homMk‚ÇÅ (Œ≤.app 1) (Œ≤.app 2) (naturality' Œ≤ 1 2) := by
    subst hŒ≤
    exact hom_ext‚ÇÅ rfl rfl
  exact (ShortComplex.LeftHomologyMapData.ofZeros
    (X.shortComplexEMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ _ _ _ _ _ _ Œ≤) _ _ _ _).homologyMap_comm

end

end SpectralObject

end Abelian

end CategoryTheory
