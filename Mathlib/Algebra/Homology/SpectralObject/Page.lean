/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.Algebra.Homology.SpectralObject.Cycles
public import Mathlib.Algebra.Homology.ShortComplex.ShortExact
public import Mathlib.CategoryTheory.Abelian.Refinements
public import Mathlib.CategoryTheory.ComposableArrows.Three
public import Batteries.Tactic.Lint

/-!
# Spectral objects in abelian categories

Let `X` be a spectral object index by the category `Œπ`
in the abelian category `C`. The purpose of this file
is to introduce the homology `X.E` of the short complex `X.shortComplexE`
`(X.H n‚ÇÄ).obj (mk‚ÇÅ f‚ÇÉ) ‚ü∂ (X.H n‚ÇÅ).obj (mk‚ÇÅ f‚ÇÇ) ‚ü∂ (X.H n‚ÇÇ).obj (mk‚ÇÅ f‚ÇÅ)`
when `f‚ÇÅ`, `f‚ÇÇ` and `f‚ÇÉ` are composable morphisms in `Œπ` and the
equalities `n‚ÇÄ + 1 = n‚ÇÅ` and `n‚ÇÅ + 1 = n‚ÇÇ` hold (both maps in the
short complex are given by `X.Œ¥`). All the relevant objects in the
spectral sequence attached to spectral objects can be defined
in terms of this homology `X.E`: the objects in all pages, including
the page at infinity.

## References
* [Jean-Louis Verdier, *Des cat√©gories d√©riv√©es des cat√©gories ab√©liennes*, II.4][verdier1996]

-/

@[expose] public section

namespace CategoryTheory

open Limits ComposableArrows

namespace Abelian

variable {C Œπ : Type*} [Category C] [Category Œπ] [Abelian C]

namespace SpectralObject

variable (X : SpectralObject C Œπ)

section

variable (n‚ÇÄ n‚ÇÅ n‚ÇÇ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ) (hn‚ÇÇ : n‚ÇÅ + 1 = n‚ÇÇ)
  {i j k l : Œπ} (f‚ÇÅ : i ‚ü∂ j) (f‚ÇÇ : j ‚ü∂ k) (f‚ÇÉ : k ‚ü∂ l)

/-- The short complex consisting of the composition of
two morphisms `X.Œ¥`, given three composable morphisms `f‚ÇÅ`, `f‚ÇÇ`
and `f‚ÇÉ` in `Œπ`, and three consecutive integers. -/
@[simps]
def shortComplexE : ShortComplex C where
  X‚ÇÅ := (X.H n‚ÇÄ).obj (mk‚ÇÅ f‚ÇÉ)
  X‚ÇÇ := (X.H n‚ÇÅ).obj (mk‚ÇÅ f‚ÇÇ)
  X‚ÇÉ := (X.H n‚ÇÇ).obj (mk‚ÇÅ f‚ÇÅ)
  f := X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÇ f‚ÇÉ
  g := X.Œ¥ n‚ÇÅ n‚ÇÇ hn‚ÇÇ f‚ÇÅ f‚ÇÇ

/-- The homology of the short complex `shortComplexE` consisting of
two morphisms `X.Œ¥`. In the documentation, we shorten it as `E^n‚ÇÅ(f‚ÇÅ, f‚ÇÇ, f‚ÇÉ)` -/
noncomputable def E : C := (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).homology

lemma isZero_E_of_isZero_H (h : IsZero ((X.H n‚ÇÅ).obj (mk‚ÇÅ f‚ÇÇ))) :
    IsZero (X.E n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ) :=
  (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).exact_iff_isZero_homology.1
    (ShortComplex.exact_of_isZero_X‚ÇÇ _ h)

end

section

variable (n‚ÇÄ n‚ÇÅ n‚ÇÇ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ) (hn‚ÇÇ : n‚ÇÅ + 1 = n‚ÇÇ)
  {i j k l : Œπ}
  {i j k l : Œπ} (f‚ÇÅ : i ‚ü∂ j) (f‚ÇÇ : j ‚ü∂ k) (f‚ÇÉ : k ‚ü∂ l)
  {i' j' k' l' : Œπ} (f‚ÇÅ' : i' ‚ü∂ j') (f‚ÇÇ' : j' ‚ü∂ k') (f‚ÇÉ' : k' ‚ü∂ l')
  {i'' j'' k'' l'' : Œπ} (f‚ÇÅ'' : i'' ‚ü∂ j'') (f‚ÇÇ'' : j'' ‚ü∂ k'') (f‚ÇÉ'' : k'' ‚ü∂ l'')
  (Œ± : mk‚ÇÉ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚ü∂ mk‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ')
  (Œ≤ : mk‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' ‚ü∂ mk‚ÇÉ f‚ÇÅ'' f‚ÇÇ'' f‚ÇÉ'')
  (Œ≥ : mk‚ÇÉ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚ü∂ mk‚ÇÉ f‚ÇÅ'' f‚ÇÇ'' f‚ÇÉ'')

/-- The functoriality of `shortComplexE` with respect to morphisms
in `ComposableArrows Œπ 3`. -/
@[simps]
def shortComplexEMap :
    X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚ü∂
      X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' where
  œÑ‚ÇÅ := (X.H n‚ÇÄ).map (homMk‚ÇÅ (Œ±.app 2) (Œ±.app 3) (naturality' Œ± 2 3))
  œÑ‚ÇÇ := (X.H n‚ÇÅ).map (homMk‚ÇÅ (Œ±.app 1) (Œ±.app 2) (naturality' Œ± 1 2))
  œÑ‚ÇÉ := (X.H n‚ÇÇ).map (homMk‚ÇÅ (Œ±.app 0) (Œ±.app 1) (naturality' Œ± 0 1))
  comm‚ÇÅ‚ÇÇ := Œ¥_naturality ..
  comm‚ÇÇ‚ÇÉ := Œ¥_naturality ..

@[simp]
lemma shortComplexEMap_id :
    X.shortComplexEMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ f‚ÇÇ f‚ÇÉ (ùüô _) = ùüô _ := by
  ext
  all_goals dsimp; convert (X.H _).map_id _; cat_disch

@[reassoc, simp]
lemma shortComplexEMap_comp :
    X.shortComplexEMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ'' f‚ÇÇ'' f‚ÇÉ'' (Œ± ‚â´ Œ≤) =
    X.shortComplexEMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' Œ± ‚â´
      X.shortComplexEMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' f‚ÇÅ'' f‚ÇÇ'' f‚ÇÉ'' Œ≤ := by
  ext
  all_goals dsimp; rw [‚Üê Functor.map_comp]; congr 1; cat_disch

/-- The functoriality of `E` with respect to morphisms
in `ComposableArrows Œπ 3`. -/
noncomputable def EMap :
    X.E n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚ü∂ X.E n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' :=
  ShortComplex.homologyMap (X.shortComplexEMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' Œ±)

@[simp]
lemma EMap_id :
    X.EMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ f‚ÇÇ f‚ÇÉ (ùüô _) = ùüô _ := by
  dsimp only [EMap]
  rw [shortComplexEMap_id, ShortComplex.homologyMap_id]
  rfl

/-- Variant of `EMap_id`. -/
lemma EMap_id' (Œ± : mk‚ÇÉ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚ü∂ mk‚ÇÉ f‚ÇÅ f‚ÇÇ f‚ÇÉ) (hŒ± : Œ± = ùüô _) :
    X.EMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ f‚ÇÇ f‚ÇÉ Œ± = ùüô _ := by
  subst hŒ±
  simp only [EMap_id]

@[reassoc, simp]
lemma EMap_comp :
    X.EMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ'' f‚ÇÇ'' f‚ÇÉ'' (Œ± ‚â´ Œ≤) =
    X.EMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' Œ± ‚â´
      X.EMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' f‚ÇÅ'' f‚ÇÇ'' f‚ÇÉ'' Œ≤ := by
  dsimp only [EMap]
  rw [shortComplexEMap_comp, ShortComplex.homologyMap_comp]

lemma isIso_EMap
    (h‚ÇÄ : IsIso ((X.H n‚ÇÄ).map ((functorArrows Œπ 2 3 3).map Œ±)))
    (h‚ÇÅ : IsIso ((X.H n‚ÇÅ).map ((functorArrows Œπ 1 2 3).map Œ±)))
    (h‚ÇÇ : IsIso ((X.H n‚ÇÇ).map ((functorArrows Œπ 0 1 3).map Œ±))) :
    IsIso (X.EMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' Œ±) := by
  have : IsIso (shortComplexEMap X n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' Œ±) := by
    apply (config := { allowSynthFailures := true})
      ShortComplex.isIso_of_isIso <;> assumption
  dsimp [EMap]
  infer_instance

end

section

variable (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ)
  {i j k : Œπ} (f : i ‚ü∂ j) (g : j ‚ü∂ k)

lemma Œ¥_eq_zero_of_isIso‚ÇÅ (hf : IsIso f) :
    X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g = 0 := by
  simpa only [Preadditive.IsIso.comp_left_eq_zero] using X.zero‚ÇÉ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g _ rfl

lemma Œ¥_eq_zero_of_isIso‚ÇÇ (hg : IsIso g) :
    X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g = 0 := by
  simpa only [Preadditive.IsIso.comp_right_eq_zero] using X.zero‚ÇÅ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f g _ rfl

end

lemma isZero_H_obj_of_isIso (n : ‚Ñ§) {i j : Œπ} (f : i ‚ü∂ j) (hf : IsIso f) :
    IsZero ((X.H n).obj (mk‚ÇÅ f)) := by
  let e : mk‚ÇÅ (ùüô i) ‚âÖ mk‚ÇÅ f := isoMk‚ÇÅ (Iso.refl _) (asIso f) (by simp)
  refine IsZero.of_iso ?_ ((X.H n).mapIso e.symm)
  have h := X.zero‚ÇÇ n (ùüô i) (ùüô i) (ùüô i) (by simp)
  rw [‚Üê Functor.map_comp] at h
  rw [IsZero.iff_id_eq_zero, ‚Üê Functor.map_id, ‚Üê h]
  congr 1
  cat_disch

section

variable (n‚ÇÄ n‚ÇÅ n‚ÇÇ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ) (hn‚ÇÇ : n‚ÇÅ + 1 = n‚ÇÇ)
  {i j k l : Œπ} (f‚ÇÅ : i ‚ü∂ j) (f‚ÇÇ : j ‚ü∂ k) (f‚ÇÉ : k ‚ü∂ l)
  (f‚ÇÅ‚ÇÇ : i ‚ü∂ k) (h‚ÇÅ‚ÇÇ : f‚ÇÅ ‚â´ f‚ÇÇ = f‚ÇÅ‚ÇÇ) (f‚ÇÇ‚ÇÉ : j ‚ü∂ l) (h‚ÇÇ‚ÇÉ : f‚ÇÇ ‚â´ f‚ÇÉ = f‚ÇÇ‚ÇÉ)

/-- `E^n‚ÇÅ(f‚ÇÅ, f‚ÇÇ, f‚ÇÉ)` identifies to the cokernel
of `Œ¥ToCycles : H^{n‚ÇÄ}(f‚ÇÉ) ‚ü∂ Z^{n‚ÇÅ}(f‚ÇÅ, f‚ÇÇ)`. -/
@[simps]
noncomputable def leftHomologyDataShortComplexE :
    (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).LeftHomologyData := by
  let hi := (X.kernelSequenceCycles_exact _ _ hn‚ÇÇ f‚ÇÅ f‚ÇÇ).fIsKernel
  have : hi.lift (KernelFork.ofŒπ _ (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).zero) =
      X.Œ¥ToCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÉ :=
    Fork.IsLimit.hom_ext hi (by simpa using hi.fac _ .zero)
  refine {
  K := X.cycles n‚ÇÅ f‚ÇÅ f‚ÇÇ
  H := cokernel (X.Œ¥ToCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÉ)
  i := X.iCycles n‚ÇÅ f‚ÇÅ f‚ÇÇ
  œÄ := cokernel.œÄ _
  wi := by simp
  hi := hi
  wœÄ := by rw [this]; simp
  hœÄ := by
    refine (IsColimit.equivOfNatIsoOfIso ?_ _ _ ?_).2
      (cokernelIsCokernel (X.Œ¥ToCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÉ))
    ¬∑ exact parallelPair.ext (Iso.refl _) (Iso.refl _) (by simpa) (by simp)
    ¬∑ exact Cofork.ext (Iso.refl _)}

@[simp]
lemma leftHomologyDataShortComplexE_f' :
    (X.leftHomologyDataShortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).f' =
      X.Œ¥ToCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÉ := by
  let hi := (X.kernelSequenceCycles_exact _ _ hn‚ÇÇ f‚ÇÅ f‚ÇÇ).fIsKernel
  exact Fork.IsLimit.hom_ext hi (by simpa using hi.fac _ .zero)

noncomputable def cyclesIso :
    (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).cycles ‚âÖ X.cycles n‚ÇÅ f‚ÇÅ f‚ÇÇ :=
  (X.leftHomologyDataShortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).cyclesIso

@[reassoc (attr := simp)]
lemma cyclesIso_inv_i :
    (X.cyclesIso n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).inv ‚â´
      (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).iCycles = X.iCycles n‚ÇÅ f‚ÇÅ f‚ÇÇ :=
  ShortComplex.LeftHomologyData.cyclesIso_inv_comp_iCycles _

@[reassoc (attr := simp)]
lemma cyclesIso_hom_i :
    (X.cyclesIso n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).hom ‚â´ X.iCycles n‚ÇÅ f‚ÇÅ f‚ÇÇ =
      (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).iCycles :=
  ShortComplex.LeftHomologyData.cyclesIso_hom_comp_i _

noncomputable def œÄE : X.cycles n‚ÇÅ f‚ÇÅ f‚ÇÇ ‚ü∂ X.E n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ :=
  (X.cyclesIso n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).inv ‚â´
    (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).homologyœÄ
  deriving Epi

@[reassoc (attr := simp)]
lemma Œ¥ToCycles_cyclesIso_inv :
    X.Œ¥ToCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚â´ (X.cyclesIso n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).inv =
      (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).toCycles := by
  rw [‚Üê cancel_mono (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).iCycles, Category.assoc,
    cyclesIso_inv_i, Œ¥ToCycles_iCycles, ShortComplex.toCycles_i, shortComplexE_f]

@[reassoc (attr := simp)]
lemma Œ¥ToCycles_œÄE :
    X.Œ¥ToCycles n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚â´ X.œÄE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ = 0 := by
  simp only [œÄE, Œ¥ToCycles_cyclesIso_inv_assoc, ShortComplex.toCycles_comp_homologyœÄ]

/-- cokernelSequenceE' -/
@[simps]
noncomputable def cokernelSequenceE' : ShortComplex C :=
    ShortComplex.mk _ _ (X.Œ¥ToCycles_œÄE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ)

@[simps!]
noncomputable def cokernelSequenceE'Iso :
    X.cokernelSequenceE' n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚âÖ ShortComplex.mk _ _
        (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).toCycles_comp_homologyœÄ :=
  ShortComplex.isoMk (Iso.refl _) (X.cyclesIso n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).symm
    (Iso.refl _) (by simp) (by simp [œÄE])

lemma cokernelSequenceE'_exact :
    (X.cokernelSequenceE' n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).Exact :=
  ShortComplex.exact_of_iso (X.cokernelSequenceE'Iso n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).symm
    (ShortComplex.exact_of_g_is_cokernel _ (ShortComplex.homologyIsCokernel _))

instance : Epi (X.cokernelSequenceE' n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).g := by
  dsimp; infer_instance

/-- `E^n‚ÇÅ(f‚ÇÅ, f‚ÇÇ, f‚ÇÉ)` identifies to the kernel
of `Œ¥FromOpcycles : opZ^{n‚ÇÅ}(f‚ÇÇ, f‚ÇÉ) ‚ü∂ H^{n‚ÇÇ}(f‚ÇÅ)`. -/
@[simps]
noncomputable def rightHomologyDataShortComplexE :
    (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).RightHomologyData := by
  let hp := (X.cokernelSequenceOpcycles_exact _ _ hn‚ÇÅ f‚ÇÇ f‚ÇÉ).gIsCokernel
  have : hp.desc (CokernelCofork.ofœÄ _ (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).zero) =
      X.Œ¥FromOpcycles n‚ÇÅ n‚ÇÇ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ :=
    Cofork.IsColimit.hom_ext hp (by simpa using hp.fac _ .one)
  refine {
  Q := X.opcycles n‚ÇÅ f‚ÇÇ f‚ÇÉ
  H := kernel (X.Œ¥FromOpcycles n‚ÇÅ n‚ÇÇ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ)
  p := X.pOpcycles n‚ÇÅ f‚ÇÇ f‚ÇÉ
  Œπ := kernel.Œπ _
  wp := by simp
  hp := hp
  wŒπ := by rw [this]; simp
  hŒπ := by
    refine (IsLimit.equivOfNatIsoOfIso ?_ _ _ ?_).2
      (kernelIsKernel (X.Œ¥FromOpcycles n‚ÇÅ n‚ÇÇ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ))
    ¬∑ exact parallelPair.ext (Iso.refl _) (Iso.refl _) (by simpa) (by simp)
    ¬∑ exact Fork.ext (Iso.refl _) }

/-- rightHomologyDataShortComplexE_g' -/
@[simp]
lemma rightHomologyDataShortComplexE_g' :
    (X.rightHomologyDataShortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).g' =
      X.Œ¥FromOpcycles n‚ÇÅ n‚ÇÇ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ := by
  let hp := (X.cokernelSequenceOpcycles_exact _ _ hn‚ÇÅ f‚ÇÇ f‚ÇÉ).gIsCokernel
  exact Cofork.IsColimit.hom_ext hp (by simpa using hp.fac _ .one)

noncomputable def opcyclesIso :
    (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).opcycles ‚âÖ X.opcycles n‚ÇÅ f‚ÇÇ f‚ÇÉ :=
  (X.rightHomologyDataShortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).opcyclesIso

@[reassoc (attr := simp)]
lemma p_opcyclesIso_hom :
    (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).pOpcycles ‚â´
      (X.opcyclesIso n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).hom =
      X.pOpcycles n‚ÇÅ f‚ÇÇ f‚ÇÉ :=
  ShortComplex.RightHomologyData.pOpcycles_comp_opcyclesIso_hom _

@[reassoc (attr := simp)]
lemma p_opcyclesIso_inv :
    X.pOpcycles n‚ÇÅ f‚ÇÇ f‚ÇÉ ‚â´ (X.opcyclesIso n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).inv =
      (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).pOpcycles :=
  (X.rightHomologyDataShortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).p_comp_opcyclesIso_inv

noncomputable def ŒπE : X.E n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚ü∂ X.opcycles n‚ÇÅ f‚ÇÇ f‚ÇÉ :=
  (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).homologyŒπ ‚â´
    (X.opcyclesIso n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).hom
  deriving Mono

@[reassoc (attr := simp)]
lemma opcyclesIso_hom_Œ¥FromOpcycles :
    (X.opcyclesIso n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).hom ‚â´ X.Œ¥FromOpcycles n‚ÇÅ n‚ÇÇ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ =
      (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).fromOpcycles := by
  rw [‚Üê cancel_epi (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).pOpcycles,
    p_opcyclesIso_hom_assoc, ShortComplex.p_fromOpcycles, shortComplexE_g,
    pOpcycles_Œ¥FromOpcycles]

@[reassoc (attr := simp)]
lemma ŒπE_Œ¥FromOpcycles :
    X.ŒπE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚â´ X.Œ¥FromOpcycles n‚ÇÅ n‚ÇÇ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ = 0 := by
  simp only [ŒπE, Category.assoc, opcyclesIso_hom_Œ¥FromOpcycles,
    ShortComplex.homologyŒπ_comp_fromOpcycles]

@[reassoc (attr := simp)]
lemma œÄE_ŒπE :
    X.œÄE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚â´ X.ŒπE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ =
      X.iCycles n‚ÇÅ f‚ÇÅ f‚ÇÇ ‚â´ X.pOpcycles n‚ÇÅ f‚ÇÇ f‚ÇÉ := by
  simp [œÄE, ŒπE]

/-- kernelSequenceE' -/
@[simps]
noncomputable def kernelSequenceE' : ShortComplex C :=
    ShortComplex.mk _ _ (X.ŒπE_Œ¥FromOpcycles n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ)

@[simps!]
noncomputable def kernelSequenceE'Iso :
    X.kernelSequenceE' n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚âÖ ShortComplex.mk _ _
        (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).homologyŒπ_comp_fromOpcycles :=
  Iso.symm (ShortComplex.isoMk (Iso.refl _) (X.opcyclesIso n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ)
    (Iso.refl _) (by simp [ŒπE]) (by simp))

lemma kernelSequenceE'_exact :
    (X.kernelSequenceE' n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).Exact :=
  ShortComplex.exact_of_iso (X.kernelSequenceE'Iso n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).symm
    (ShortComplex.exact_of_f_is_kernel _ (ShortComplex.homologyIsKernel _))

instance : Mono (X.kernelSequenceE' n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).f := by
  dsimp
  infer_instance

@[simps]
noncomputable def cokernelSequenceE : ShortComplex C where
  X‚ÇÅ := (X.H n‚ÇÅ).obj (mk‚ÇÅ f‚ÇÅ) ‚äû (X.H n‚ÇÄ).obj (mk‚ÇÅ f‚ÇÉ)
  X‚ÇÇ := (X.H n‚ÇÅ).obj (mk‚ÇÅ f‚ÇÅ‚ÇÇ)
  X‚ÇÉ := X.E n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ
  f := biprod.desc ((X.H n‚ÇÅ).map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ)) (X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÉ)
  g := X.toCycles n‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ ‚â´ X.œÄE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ
  zero := by ext <;> simp

instance : Epi (X.cokernelSequenceE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ).g := by
  dsimp
  apply epi_comp

lemma cokernelSequenceE_exact :
    (X.cokernelSequenceE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ).Exact := by
  rw [ShortComplex.exact_iff_exact_up_to_refinements]
  intro A x‚ÇÇ hx‚ÇÇ
  dsimp at x‚ÇÇ hx‚ÇÇ
  obtain ‚ü®A‚ÇÅ, œÄ‚ÇÅ, _, y‚ÇÅ, hy‚ÇÅ‚ü© :=
    (X.cokernelSequenceE'_exact n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).exact_up_to_refinements
      (x‚ÇÇ ‚â´ X.toCycles n‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ) (by simpa using hx‚ÇÇ)
  dsimp at y‚ÇÅ hy‚ÇÅ
  let z := œÄ‚ÇÅ ‚â´ x‚ÇÇ - y‚ÇÅ ‚â´ X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÉ
  obtain ‚ü®A‚ÇÇ, œÄ‚ÇÇ, _, x‚ÇÅ, hx‚ÇÅ‚ü© := (X.exact‚ÇÇ n‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ).exact_up_to_refinements z (by
      have : z ‚â´ X.toCycles n‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ = 0 := by simp [z, hy‚ÇÅ]
      simpa only [zero_comp, Category.assoc, toCycles_i] using this =‚â´ X.iCycles n‚ÇÅ f‚ÇÅ f‚ÇÇ)
  dsimp at x‚ÇÅ hx‚ÇÅ
  exact ‚ü®A‚ÇÇ, œÄ‚ÇÇ ‚â´ œÄ‚ÇÅ, epi_comp _ _, biprod.lift x‚ÇÅ (œÄ‚ÇÇ ‚â´ y‚ÇÅ), by simp [z, ‚Üê hx‚ÇÅ]‚ü©

section

variable {A : C} (x : (X.H n‚ÇÅ).obj (mk‚ÇÅ f‚ÇÅ‚ÇÇ) ‚ü∂ A)
  (h : (X.H n‚ÇÅ).map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ) ‚â´ x = 0)
  (h' : X.Œ¥ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÉ ‚â´ x = 0)

noncomputable def descE :
    X.E n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚ü∂ A :=
  (X.cokernelSequenceE_exact n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ).desc x (by cat_disch)

@[reassoc (attr := simp)]
lemma toCycles_œÄE_descE :
    X.toCycles n‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ ‚â´ X.œÄE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚â´
      X.descE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ x h h' = x := by
  dsimp only [descE]
  rw [‚Üê Category.assoc]
  apply (X.cokernelSequenceE_exact n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ).g_desc

end

@[simps]
noncomputable def kernelSequenceE : ShortComplex C where
  X‚ÇÅ := X.E n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ
  X‚ÇÇ := (X.H n‚ÇÅ).obj (mk‚ÇÅ f‚ÇÇ‚ÇÉ)
  X‚ÇÉ := (X.H n‚ÇÅ).obj (mk‚ÇÅ f‚ÇÉ) ‚äû (X.H n‚ÇÇ).obj (mk‚ÇÅ f‚ÇÅ)
  f := X.ŒπE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚â´ X.fromOpcycles n‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÇ‚ÇÉ h‚ÇÇ‚ÇÉ
  g := biprod.lift ((X.H n‚ÇÅ).map (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f‚ÇÇ f‚ÇÉ f‚ÇÇ‚ÇÉ h‚ÇÇ‚ÇÉ)) (X.Œ¥ n‚ÇÅ n‚ÇÇ hn‚ÇÇ f‚ÇÅ f‚ÇÇ‚ÇÉ)
  zero := by ext <;> simp

instance : Mono (X.kernelSequenceE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÇ‚ÇÉ h‚ÇÇ‚ÇÉ).f := by
  dsimp
  infer_instance

lemma kernelSequenceE_exact :
    (X.kernelSequenceE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÇ‚ÇÉ h‚ÇÇ‚ÇÉ).Exact := by
  rw [ShortComplex.exact_iff_exact_up_to_refinements]
  intro A x‚ÇÇ hx‚ÇÇ
  dsimp at x‚ÇÇ hx‚ÇÇ
  obtain ‚ü®A‚ÇÅ, œÄ‚ÇÅ, _, x‚ÇÅ, hx‚ÇÅ‚ü© :=
    (X.kernelSequenceE'_exact n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).exact_up_to_refinements
      (X.liftOpcycles n‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÇ‚ÇÉ h‚ÇÇ‚ÇÉ x‚ÇÇ (by simpa using hx‚ÇÇ =‚â´ biprod.fst)) (by
        dsimp
        rw [‚Üê X.fromOpcyles_Œ¥ n‚ÇÅ n‚ÇÇ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÇ‚ÇÉ h‚ÇÇ‚ÇÉ,
          X.liftOpcycles_fromOpcycles_assoc ]
        simpa using hx‚ÇÇ =‚â´ biprod.snd)
  dsimp at x‚ÇÅ hx‚ÇÅ
  refine ‚ü®A‚ÇÅ, œÄ‚ÇÅ, inferInstance, x‚ÇÅ, ?_‚ü©
  dsimp
  rw [‚Üê reassoc_of% hx‚ÇÅ, liftOpcycles_fromOpcycles]

section

variable {A : C} (x : A ‚ü∂ (X.H n‚ÇÅ).obj (mk‚ÇÅ f‚ÇÇ‚ÇÉ))
  (h : x ‚â´ (X.H n‚ÇÅ).map (twoŒ¥‚ÇÅToŒ¥‚ÇÄ f‚ÇÇ f‚ÇÉ f‚ÇÇ‚ÇÉ h‚ÇÇ‚ÇÉ) = 0)
  (h' : x ‚â´ X.Œ¥ n‚ÇÅ n‚ÇÇ hn‚ÇÇ f‚ÇÅ f‚ÇÇ‚ÇÉ = 0)

noncomputable def liftE :
    A ‚ü∂ X.E n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ :=
  (X.kernelSequenceE_exact n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÇ‚ÇÉ h‚ÇÇ‚ÇÉ).lift x (by cat_disch)

@[reassoc (attr := simp)]
lemma liftE_ŒπE_fromOpcycles :
    X.liftE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÇ‚ÇÉ h‚ÇÇ‚ÇÉ x h h' ‚â´ X.ŒπE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚â´
      X.fromOpcycles n‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÇ‚ÇÉ h‚ÇÇ‚ÇÉ = x := by
  apply (X.kernelSequenceE_exact n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÇ‚ÇÉ h‚ÇÇ‚ÇÉ).lift_f

end

end

section

variable (n‚ÇÄ n‚ÇÅ n‚ÇÇ : ‚Ñ§)
  (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ) (hn‚ÇÇ : n‚ÇÅ + 1 = n‚ÇÇ)
  {i‚ÇÄ i‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ}
  (f‚ÇÅ : i‚ÇÄ ‚ü∂ i‚ÇÅ) (f‚ÇÇ : i‚ÇÅ ‚ü∂ i‚ÇÇ) (f‚ÇÉ : i‚ÇÇ ‚ü∂ i‚ÇÉ)
  {i‚ÇÄ' i‚ÇÅ' i‚ÇÇ' i‚ÇÉ' : Œπ}
  (f‚ÇÅ' : i‚ÇÄ' ‚ü∂ i‚ÇÅ') (f‚ÇÇ' : i‚ÇÅ' ‚ü∂ i‚ÇÇ') (f‚ÇÉ' : i‚ÇÇ' ‚ü∂ i‚ÇÉ')
  (Œ± : mk‚ÇÉ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚ü∂ mk‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ')

@[reassoc]
lemma cyclesIso_inv_cyclesMap
    (Œ≤ : mk‚ÇÇ f‚ÇÅ f‚ÇÇ ‚ü∂ mk‚ÇÇ f‚ÇÅ' f‚ÇÇ')
    (hŒ≤ : Œ≤ = homMk‚ÇÇ (Œ±.app 0) (Œ±.app 1) (Œ±.app 2) (naturality' Œ± 0 1 (by lia) (by lia))
      (naturality' Œ± 1 2 (by lia) (by lia))) :
    (X.cyclesIso n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).inv ‚â´
      ShortComplex.cyclesMap (X.shortComplexEMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' Œ±) =
      X.cyclesMap n‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÅ' f‚ÇÇ' Œ≤ ‚â´
        (X.cyclesIso n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ').inv := by
  subst hŒ≤
  rw [‚Üê cancel_mono (ShortComplex.iCycles _), Category.assoc, Category.assoc,
    ShortComplex.cyclesMap_i, cyclesIso_inv_i_assoc, cyclesIso_inv_i,
    shortComplexEMap_œÑ‚ÇÇ, cyclesMap_i]
  dsimp

@[reassoc]
lemma opcyclesMap_opcyclesIso_hom
    (Œ≥ : mk‚ÇÇ f‚ÇÇ f‚ÇÉ ‚ü∂ mk‚ÇÇ f‚ÇÇ' f‚ÇÉ')
    (hŒ≥ : Œ≥ = homMk‚ÇÇ (Œ±.app 1) (Œ±.app 2) (Œ±.app 3) (naturality' Œ± 1 2) (naturality' Œ± 2 3)) :
    ShortComplex.opcyclesMap (X.shortComplexEMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' Œ±) ‚â´
      (X.opcyclesIso n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ').hom =
    (X.opcyclesIso n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ).hom ‚â´ X.opcyclesMap n‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÇ' f‚ÇÉ' Œ≥ := by
  subst hŒ≥
  rw [‚Üê cancel_epi (ShortComplex.pOpcycles _), ShortComplex.p_opcyclesMap_assoc,
    p_opcyclesIso_hom, p_opcyclesIso_hom_assoc, shortComplexEMap_œÑ‚ÇÇ, p_opcyclesMap]
  dsimp

@[reassoc]
lemma œÄE_EMap (Œ≤ : mk‚ÇÇ f‚ÇÅ f‚ÇÇ ‚ü∂ mk‚ÇÇ f‚ÇÅ' f‚ÇÇ')
    (hŒ≤ : Œ≤ = homMk‚ÇÇ (Œ±.app 0) (Œ±.app 1) (Œ±.app 2) (naturality' Œ± 0 1 (by lia) (by lia))
    (naturality' Œ± 1 2 (by lia) (by lia))) :
    X.œÄE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚â´ X.EMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' Œ± =
      X.cyclesMap n‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÅ' f‚ÇÇ' Œ≤ ‚â´ X.œÄE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' := by
  dsimp [œÄE, EMap]
  simp only [Category.assoc, ShortComplex.homologyœÄ_naturality,
    X.cyclesIso_inv_cyclesMap_assoc n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' Œ± Œ≤ hŒ≤]

@[reassoc]
lemma EMap_ŒπE
    (Œ≥ : mk‚ÇÇ f‚ÇÇ f‚ÇÉ ‚ü∂ mk‚ÇÇ f‚ÇÇ' f‚ÇÉ')
    (hŒ≥ : Œ≥ = homMk‚ÇÇ (Œ±.app 1) (Œ±.app 2) (Œ±.app 3) (naturality' Œ± 1 2) (naturality' Œ± 2 3)) :
    X.EMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' Œ± ‚â´ X.ŒπE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' =
      X.ŒπE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚â´ X.opcyclesMap n‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÇ' f‚ÇÉ' Œ≥ := by
  dsimp [ŒπE, EMap]
  simp only [ShortComplex.homologyŒπ_naturality_assoc, Category.assoc,
    X.opcyclesMap_opcyclesIso_hom n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' Œ± Œ≥ hŒ≥]

end

section

variable (n‚ÇÄ n‚ÇÅ n‚ÇÇ : ‚Ñ§)
  (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ) (hn‚ÇÇ : n‚ÇÅ + 1 = n‚ÇÇ)
  {i‚ÇÄ i‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ}
  (f‚ÇÅ : i‚ÇÄ ‚ü∂ i‚ÇÅ) (f‚ÇÇ : i‚ÇÅ ‚ü∂ i‚ÇÇ) (f‚ÇÉ : i‚ÇÇ ‚ü∂ i‚ÇÉ)
  (f‚ÇÅ‚ÇÇ : i‚ÇÄ ‚ü∂ i‚ÇÇ) (h‚ÇÅ‚ÇÇ : f‚ÇÅ ‚â´ f‚ÇÇ = f‚ÇÅ‚ÇÇ)

noncomputable def opcyclesToE : X.opcycles n‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÉ ‚ü∂ X.E n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ :=
  X.descOpcycles n‚ÇÄ _ hn‚ÇÅ _ _
    (X.toCycles n‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ ‚â´ X.œÄE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ) (by simp)

@[reassoc (attr := simp)]
lemma p_opcyclesToE :
    X.pOpcycles n‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÉ ‚â´ X.opcyclesToE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ =
      X.toCycles n‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ ‚â´ X.œÄE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ := by
  simp [opcyclesToE]

@[reassoc (attr := simp)]
lemma opcyclesToE_ŒπE :
    X.opcyclesToE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ ‚â´ X.ŒπE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ =
      X.opcyclesMap n‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÉ f‚ÇÇ f‚ÇÉ (threeŒ¥‚ÇÅToŒ¥‚ÇÄ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ) := by
  rw [‚Üê cancel_epi (X.pOpcycles n‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÉ), p_opcyclesToE_assoc,
    œÄE_ŒπE, toCycles_i_assoc]
  symm
  apply X.p_opcyclesMap
  rfl

instance : Epi (X.opcyclesToE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ) :=
  epi_of_epi_fac (X.p_opcyclesToE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ)

/-- cokernelSequenceE'' -/
@[simps!]
noncomputable def cokernelSequenceE'' : ShortComplex C where
  X‚ÇÅ := (X.H n‚ÇÅ).obj (mk‚ÇÅ f‚ÇÅ)
  X‚ÇÇ := X.opcycles n‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÉ
  X‚ÇÉ := X.E n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ
  f := (X.H n‚ÇÅ).map (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ) ‚â´ X.pOpcycles n‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÉ
  g := X.opcyclesToE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ

instance : Epi (X.cokernelSequenceE'' n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ).g := by
  dsimp
  infer_instance

lemma cokernelSequenceE''_exact :
    (X.cokernelSequenceE'' n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ).Exact := by
  rw [ShortComplex.exact_iff_exact_up_to_refinements]
  intro A x‚ÇÇ hx‚ÇÇ
  dsimp at x‚ÇÇ hx‚ÇÇ
  obtain ‚ü®A‚ÇÅ, œÄ‚ÇÅ, _, y‚ÇÇ, hy‚ÇÇ‚ü© :=
    surjective_up_to_refinements_of_epi (X.pOpcycles n‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÉ) x‚ÇÇ
  obtain ‚ü®A‚ÇÇ, œÄ‚ÇÇ, _, y‚ÇÅ, hy‚ÇÅ‚ü© :=
    (X.cokernelSequenceE_exact n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ).exact_up_to_refinements y‚ÇÇ
      (by simpa only [Category.assoc, p_opcyclesToE, hx‚ÇÇ, comp_zero]
        using hy‚ÇÇ.symm =‚â´ X.opcyclesToE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ)
  dsimp at y‚ÇÅ hy‚ÇÅ
  obtain ‚ü®a, b, rfl‚ü© : ‚àÉ a b, y‚ÇÅ = a ‚â´ biprod.inl + b ‚â´ biprod.inr :=
    ‚ü®y‚ÇÅ ‚â´ biprod.fst, y‚ÇÅ ‚â´ biprod.snd, by ext <;> simp‚ü©
  simp only [Preadditive.add_comp, Category.assoc, biprod.inl_desc, biprod.inr_desc] at hy‚ÇÅ
  refine ‚ü®A‚ÇÇ, œÄ‚ÇÇ ‚â´ œÄ‚ÇÅ, inferInstance, a, ?_‚ü©
  dsimp
  simp only [Category.assoc, hy‚ÇÇ, reassoc_of% hy‚ÇÅ, Preadditive.add_comp, Œ¥_pOpcycles,
    comp_zero, add_zero]

-- TODO: dual statement?

end

section

variable (n‚ÇÄ n‚ÇÅ n‚ÇÇ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ) (hn‚ÇÇ : n‚ÇÅ + 1 = n‚ÇÇ)
  {i j : Œπ} (f : i ‚ü∂ j) {i' j' : Œπ} (f' : i' ‚ü∂ j')

/-- An homology data for `X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ (ùüô i) f (ùüô j)`,
expressing `H^n‚ÇÅ(f)` as the homology of this short complex,
see `EIsoH`. -/
@[simps!]
noncomputable def homologyDataEIdId :
    (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ (ùüô i) f (ùüô j)).HomologyData :=
  (ShortComplex.HomologyData.ofZeros (X.shortComplexE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ (ùüô i) f (ùüô j))
    (X.Œ¥_eq_zero_of_isIso‚ÇÇ n‚ÇÄ n‚ÇÅ hn‚ÇÅ f (ùüô j) inferInstance)
    (X.Œ¥_eq_zero_of_isIso‚ÇÅ n‚ÇÅ n‚ÇÇ hn‚ÇÇ (ùüô i) f inferInstance))

/-- For any morphism `f : i ‚ü∂ j`, this is the isomorphism from
`E^n‚ÇÅ(ùüô i, f, ùüô j)` to `H^n‚ÇÅ(f)`. -/
noncomputable def EIsoH :
    X.E n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ (ùüô i) f (ùüô j) ‚âÖ (X.H n‚ÇÅ).obj (mk‚ÇÅ f) :=
  (X.homologyDataEIdId ..).left.homologyIso

lemma EIsoH_hom_naturality
    (Œ± : mk‚ÇÅ f ‚ü∂ mk‚ÇÅ f') (Œ≤ : mk‚ÇÉ (ùüô _) f (ùüô _) ‚ü∂ mk‚ÇÉ (ùüô _) f' (ùüô _))
    (hŒ≤ : Œ≤ = homMk‚ÇÉ (Œ±.app 0) (Œ±.app 0) (Œ±.app 1) (Œ±.app 1)
      (by simp) (naturality' Œ± 0 1) (by simp [Precomp.obj, Precomp.map])) :
    X.EMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ (ùüô _) f (ùüô _) (ùüô _) f' (ùüô _) Œ≤ ‚â´
      (X.EIsoH n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f').hom =
    (X.EIsoH n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f).hom ‚â´ (X.H n‚ÇÅ).map Œ± := by
  obtain rfl : Œ± = homMk‚ÇÅ (Œ≤.app 1) (Œ≤.app 2) (naturality' Œ≤ 1 2) := by
    subst hŒ≤
    exact hom_ext‚ÇÅ rfl rfl
  exact (ShortComplex.LeftHomologyMapData.ofZeros
    (X.shortComplexEMap n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ _ _ _ _ _ _ Œ≤) _ _ _ _).homologyMap_comm

end

section

variable (n‚ÇÄ n‚ÇÅ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ)
  {i‚ÇÄ i‚ÇÅ : Œπ} (f : i‚ÇÄ ‚ü∂ i‚ÇÅ)

noncomputable def cyclesIsoH :
    X.cycles n‚ÇÄ (ùüô i‚ÇÄ) f ‚âÖ (X.H n‚ÇÄ).obj (mk‚ÇÅ f) :=
  (X.cyclesIso (n‚ÇÄ - 1) n‚ÇÄ n‚ÇÅ (by lia) hn‚ÇÅ (ùüô i‚ÇÄ) f (ùüô i‚ÇÅ)).symm ‚â™‚â´
    (X.homologyDataEIdId ..).left.cyclesIso

@[simp]
lemma cyclesIsoH_inv :
    (X.cyclesIsoH n‚ÇÄ n‚ÇÅ hn‚ÇÅ f).inv = X.toCycles n‚ÇÄ (ùüô _) f f (by simp) := by
  rw [‚Üê cancel_mono (X.iCycles n‚ÇÄ (ùüô _) f ), toCycles_i]
  dsimp [cyclesIsoH]
  rw [Category.assoc, cyclesIso_hom_i,
    ShortComplex.LeftHomologyData.cyclesIso_inv_comp_iCycles,
    homologyDataEIdId_left_i, ‚Üê Functor.map_id]
  congr 1
  cat_disch

@[reassoc (attr := simp)]
lemma cyclesIsoH_hom_inv_id :
    (X.cyclesIsoH n‚ÇÄ n‚ÇÅ hn‚ÇÅ f).hom ‚â´
      X.toCycles n‚ÇÄ (ùüô _) f f (by simp) = ùüô _ := by
  simpa using (X.cyclesIsoH n‚ÇÄ n‚ÇÅ hn‚ÇÅ f).hom_inv_id

@[reassoc (attr := simp)]
lemma cyclesIsoH_inv_hom_id :
    X.toCycles n‚ÇÄ (ùüô _) f f (by simp) ‚â´
      (X.cyclesIsoH n‚ÇÄ n‚ÇÅ hn‚ÇÅ f).hom = ùüô _ := by
  simpa using (X.cyclesIsoH n‚ÇÄ n‚ÇÅ hn‚ÇÅ f).inv_hom_id

noncomputable def opcyclesIsoH :
    X.opcycles n‚ÇÅ f (ùüô i‚ÇÅ) ‚âÖ (X.H n‚ÇÅ).obj (mk‚ÇÅ f) :=
  (X.opcyclesIso n‚ÇÄ n‚ÇÅ (n‚ÇÅ + 1) hn‚ÇÅ (by lia) (ùüô i‚ÇÄ) f (ùüô i‚ÇÅ)).symm ‚â™‚â´
    (X.homologyDataEIdId ..).right.opcyclesIso

@[simp]
lemma opcyclesIsoH_hom :
    (X.opcyclesIsoH n‚ÇÄ n‚ÇÅ hn‚ÇÅ f).hom = X.fromOpcycles n‚ÇÅ f (ùüô _) f (by simp) := by
  rw [‚Üê cancel_epi (X.pOpcycles n‚ÇÅ f (ùüô _)), p_fromOpcycles]
  dsimp [opcyclesIsoH]
  rw [p_opcyclesIso_inv_assoc, ShortComplex.RightHomologyData.pOpcycles_comp_opcyclesIso_hom,
    homologyDataEIdId_right_p, ‚Üê Functor.map_id]
  congr 1
  cat_disch

@[reassoc (attr := simp)]
lemma opcyclesIsoH_hom_inv_id :
      X.fromOpcycles n‚ÇÅ f (ùüô _) f (by simp) ‚â´
        (X.opcyclesIsoH n‚ÇÄ n‚ÇÅ hn‚ÇÅ f).inv = ùüô _ := by
  simpa using (X.opcyclesIsoH n‚ÇÄ n‚ÇÅ hn‚ÇÅ f).hom_inv_id

@[reassoc (attr := simp)]
lemma opcyclesIsoH_inv_hom_id :
    (X.opcyclesIsoH n‚ÇÄ n‚ÇÅ hn‚ÇÅ f).inv ‚â´
      X.fromOpcycles n‚ÇÅ f (ùüô _) f (by simp) = ùüô _ := by
  simpa using (X.opcyclesIsoH n‚ÇÄ n‚ÇÅ hn‚ÇÅ f).inv_hom_id

end

section

variable (n‚ÇÄ n‚ÇÅ n‚ÇÇ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ) (hn‚ÇÇ : n‚ÇÅ + 1 = n‚ÇÇ) {i j : Œπ} (f : i ‚ü∂ j)

@[reassoc (attr := simp)]
lemma cyclesIsoH_hom_EIsoH_inv :
    (X.cyclesIsoH n‚ÇÅ n‚ÇÇ hn‚ÇÇ f).hom ‚â´ (X.EIsoH n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f).inv =
      X.œÄE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ (ùüô i) f (ùüô j) := by
  let h := (X.homologyDataEIdId n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f).left
  have : h.cyclesIso.inv =
      X.toCycles n‚ÇÅ (ùüô i) f f (by simp) ‚â´
        (X.cyclesIso n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ (ùüô i) f (ùüô j)).inv := by
    rw [‚Üê cancel_mono (X.cyclesIso ..).hom,
      Category.assoc, Iso.inv_hom_id, Category.comp_id,
      ‚Üê cancel_mono (X.iCycles ..), Category.assoc, cyclesIso_hom_i,
      h.cyclesIso_inv_comp_iCycles, toCycles_i]
    dsimp [h]
    rw [‚Üê Functor.map_id]
    congr 1
    cat_disch
  obtain rfl : n‚ÇÄ = n‚ÇÅ - 1 := by lia
  rw [‚Üê cancel_epi (X.cyclesIsoH n‚ÇÅ n‚ÇÇ hn‚ÇÇ f).inv,
    cyclesIsoH_inv, cyclesIsoH_inv_hom_id_assoc]
  dsimp [EIsoH]
  rw [‚Üê cancel_epi h.œÄ, h.œÄ_comp_homologyIso_inv]
  simp [œÄE, h, this]

@[reassoc (attr := simp)]
lemma EIsoH_hom_opcyclesIsoH_inv :
    (X.EIsoH n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f).hom ‚â´ (X.opcyclesIsoH n‚ÇÄ n‚ÇÅ hn‚ÇÅ f).inv =
      X.ŒπE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ (ùüô i) f (ùüô j) := by
  let h := (X.homologyDataEIdId n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f)
  have : h.right.opcyclesIso.hom =
      (X.opcyclesIso n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ (ùüô i) f (ùüô j)).hom ‚â´
        X.fromOpcycles n‚ÇÅ f (ùüô j) f (by simp) := by
    rw [‚Üê cancel_epi (X.opcyclesIso ..).inv, Iso.inv_hom_id_assoc,
      ‚Üê cancel_epi (X.pOpcycles ..), p_opcyclesIso_inv_assoc,
      h.right.pOpcycles_comp_opcyclesIso_hom, p_fromOpcycles]
    dsimp [h]
    rw [‚Üê Functor.map_id]
    congr 1
    cat_disch
  obtain rfl : n‚ÇÇ = n‚ÇÅ + 1 := by lia
  rw [‚Üê cancel_mono (X.opcyclesIsoH n‚ÇÄ n‚ÇÅ hn‚ÇÅ f).hom, Category.assoc,
    opcyclesIsoH_hom, opcyclesIsoH_inv_hom_id]
  dsimp [EIsoH, ŒπE]
  rw [Category.assoc, ‚Üê this,
    h.left_homologyIso_eq_right_homologyIso_trans_iso_symm,
    ‚Üê ShortComplex.RightHomologyData.homologyIso_hom_comp_Œπ]
  simp [h]

end

section

variable (n‚ÇÄ n‚ÇÅ n‚ÇÇ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ) (hn‚ÇÇ : n‚ÇÅ + 1 = n‚ÇÇ)
    {i‚ÇÄ i‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ} (f‚ÇÅ : i‚ÇÄ ‚ü∂ i‚ÇÅ) (f‚ÇÇ : i‚ÇÅ ‚ü∂ i‚ÇÇ) (f‚ÇÉ : i‚ÇÇ ‚ü∂ i‚ÇÉ)
    (f‚ÇÅ‚ÇÇ : i‚ÇÄ ‚ü∂ i‚ÇÇ) (f‚ÇÇ‚ÇÉ : i‚ÇÅ ‚ü∂ i‚ÇÉ)
    (h‚ÇÅ‚ÇÇ : f‚ÇÅ ‚â´ f‚ÇÇ = f‚ÇÅ‚ÇÇ) (h‚ÇÇ‚ÇÉ : f‚ÇÇ ‚â´ f‚ÇÉ = f‚ÇÇ‚ÇÉ)

@[reassoc (attr := simp)]
lemma opcyclesMap_threeŒ¥‚ÇÇToŒ¥‚ÇÅ_opcyclesToE :
    X.opcyclesMap n‚ÇÅ _ _ _ _ (threeŒ¥‚ÇÇToŒ¥‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ) ‚â´
      X.opcyclesToE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ = 0 := by
  rw [‚Üê cancel_epi (X.pOpcycles ..), comp_zero,
    p_opcyclesMap_assoc _ _ _ _ _ _ _ (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ) rfl _,
    p_opcyclesToE, H_map_twoŒ¥‚ÇÇToŒ¥‚ÇÅ_toCycles_assoc, zero_comp]

@[simps]
noncomputable def shortComplexOpcyclesThreeŒ¥‚ÇÇToŒ¥‚ÇÅ : ShortComplex C :=
  ShortComplex.mk _ _
    (X.opcyclesMap_threeŒ¥‚ÇÇToŒ¥‚ÇÅ_opcyclesToE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ)

instance :
    Mono (X.shortComplexOpcyclesThreeŒ¥‚ÇÇToŒ¥‚ÇÅ n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ).f := by
  dsimp
  rw [Preadditive.mono_iff_cancel_zero]
  intro A x hx
  replace hx := hx =‚â´ X.fromOpcycles n‚ÇÅ f‚ÇÅ‚ÇÇ f‚ÇÉ _ rfl
  rw [zero_comp, Category.assoc,
    X.opcyclesMap_fromOpcycles n‚ÇÅ f‚ÇÅ f‚ÇÇ‚ÇÉ f‚ÇÅ‚ÇÇ f‚ÇÉ (f‚ÇÅ‚ÇÇ ‚â´ f‚ÇÉ) (by cat_disch) _ rfl _ (ùüô _)
      (by simp) (by cat_disch), Functor.map_id, Category.comp_id] at hx
  rw [‚Üê cancel_mono (X.fromOpcycles n‚ÇÅ f‚ÇÅ f‚ÇÇ‚ÇÉ (f‚ÇÅ‚ÇÇ ‚â´ f‚ÇÉ) (by cat_disch)), hx, zero_comp]

instance :
    Epi (X.shortComplexOpcyclesThreeŒ¥‚ÇÇToŒ¥‚ÇÅ n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ).g := by
  dsimp; infer_instance

lemma shortComplexOpcyclesThreeŒ¥‚ÇÇToŒ¥‚ÇÅ_exact :
    (X.shortComplexOpcyclesThreeŒ¥‚ÇÇToŒ¥‚ÇÅ n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ).Exact := by
  let œÜ : X.cokernelSequenceE'' n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ ‚ü∂
      (X.shortComplexOpcyclesThreeŒ¥‚ÇÇToŒ¥‚ÇÅ n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ) :=
    { œÑ‚ÇÅ := X.pOpcycles n‚ÇÅ f‚ÇÅ f‚ÇÇ‚ÇÉ
      œÑ‚ÇÇ := ùüô _
      œÑ‚ÇÉ := ùüô _
      comm‚ÇÅ‚ÇÇ := by
        dsimp
        rw [Category.comp_id, X.p_opcyclesMap _ _ _ _ _ _ (twoŒ¥‚ÇÇToŒ¥‚ÇÅ f‚ÇÅ f‚ÇÇ f‚ÇÅ‚ÇÇ) rfl] }
  rw [‚Üê ShortComplex.exact_iff_of_epi_of_isIso_of_mono œÜ]
  exact X.cokernelSequenceE''_exact n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ

lemma shortComplexOpcyclesThreeŒ¥‚ÇÇToŒ¥‚ÇÅ_shortExact :
    (X.shortComplexOpcyclesThreeŒ¥‚ÇÇToŒ¥‚ÇÅ n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ f‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ).ShortExact where
  exact := X.shortComplexOpcyclesThreeŒ¥‚ÇÇToŒ¥‚ÇÅ_exact ..

end

variable (n‚ÇÄ n‚ÇÅ n‚ÇÇ : ‚Ñ§) (hn‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ) (hn‚ÇÇ : n‚ÇÅ + 1 = n‚ÇÇ)
    {i‚ÇÄ i‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ} (f‚ÇÅ : i‚ÇÄ ‚ü∂ i‚ÇÅ) (f‚ÇÇ : i‚ÇÅ ‚ü∂ i‚ÇÇ) (f‚ÇÉ : i‚ÇÇ ‚ü∂ i‚ÇÉ)
    (f‚ÇÅ‚ÇÇ : i‚ÇÄ ‚ü∂ i‚ÇÇ) (h‚ÇÅ‚ÇÇ : f‚ÇÅ ‚â´ f‚ÇÇ = f‚ÇÅ‚ÇÇ)
    {i‚ÇÄ' i‚ÇÅ' i‚ÇÇ' i‚ÇÉ' : Œπ} (f‚ÇÅ' : i‚ÇÄ' ‚ü∂ i‚ÇÅ') (f‚ÇÇ' : i‚ÇÅ' ‚ü∂ i‚ÇÇ') (f‚ÇÉ' : i‚ÇÇ' ‚ü∂ i‚ÇÉ')
    (f‚ÇÅ‚ÇÇ' : i‚ÇÄ' ‚ü∂ i‚ÇÇ') (h‚ÇÅ‚ÇÇ' : f‚ÇÅ' ‚â´ f‚ÇÇ' = f‚ÇÅ‚ÇÇ')

@[reassoc]
lemma opcyclesToE_EMap (Œ± : mk‚ÇÉ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚ü∂ mk‚ÇÉ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ') (Œ≤ : mk‚ÇÇ f‚ÇÅ‚ÇÇ f‚ÇÉ ‚ü∂ mk‚ÇÇ f‚ÇÅ‚ÇÇ' f‚ÇÉ')
    (h‚ÇÄ : Œ≤.app 0 = Œ±.app 0) (h‚ÇÅ : Œ≤.app 1 = Œ±.app 2) :
    X.opcyclesToE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÉ f‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ ‚â´ X.EMap _ _ _ _ _ _ _ _ _ _ _ Œ± =
      X.opcyclesMap _ _ _ _ _ Œ≤ ‚â´ X.opcyclesToE n‚ÇÄ n‚ÇÅ n‚ÇÇ hn‚ÇÅ hn‚ÇÇ f‚ÇÅ' f‚ÇÇ' f‚ÇÉ' f‚ÇÅ‚ÇÇ' h‚ÇÅ‚ÇÇ' := by
  rw [‚Üê cancel_mono (X.ŒπE ..), Category.assoc, Category.assoc, opcyclesToE_ŒπE,
    ‚Üê cancel_epi (X.pOpcycles ..), p_opcyclesToE_assoc,
    X.œÄE_EMap_assoc _ _ _ _ _ _ _ _ _ _ _ _
      (homMk‚ÇÇ (Œ±.app 0) (Œ±.app 1) (Œ±.app 2) (naturality' Œ± 0 1) (naturality' Œ± 1 2)) rfl,
    œÄE_ŒπE, X.cyclesMap_i_assoc _ _ _ _ _ _ _ rfl, toCycles_i_assoc,
    X.p_opcyclesMap_assoc _ _ _ _ _ _ _ rfl, X.p_opcyclesMap _ _ _ _ _ _ _ rfl,
    ‚Üê Functor.map_comp_assoc, ‚Üê Functor.map_comp_assoc]
  congr 2
  ext
  ¬∑ simpa [h‚ÇÄ] using naturality' Œ± 0 1
  ¬∑ simp [h‚ÇÅ]

end SpectralObject

end Abelian

end CategoryTheory
