/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Homology.TotalComplex
import Mathlib.CategoryTheory.GradedObject.Bifunctor

/-!
# The action of a bifunctor on homological complexes

Given a bifunctor `F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D` and complexes shapes `c‚ÇÅ : ComplexShape I‚ÇÅ` and
`c‚ÇÇ : ComplexShape I‚ÇÇ`, we define a bifunctor `mapBifunctorHomologicalComplex F c‚ÇÅ c‚ÇÇ`
of type `HomologicalComplex C‚ÇÅ c‚ÇÅ ‚•§ HomologicalComplex C‚ÇÇ c‚ÇÇ ‚•§ HomologicalComplex‚ÇÇ D c‚ÇÅ c‚ÇÇ`.

Then, when `K‚ÇÅ : HomologicalComplex C‚ÇÅ c‚ÇÅ`, `K‚ÇÇ : HomologicalComplex C‚ÇÇ c‚ÇÇ` and
`c : ComplexShape J` are such that we have `TotalComplexShape c‚ÇÅ c‚ÇÇ c`, we introduce
a typeclass `HasMapBifunctor K‚ÇÅ K‚ÇÇ F c` which allows to define
`mapBifunctor K‚ÇÅ K‚ÇÇ F c : HomologicalComplex D c` as the total complex of the
bicomplex `(((F.mapBifunctorHomologicalComplex c‚ÇÅ c‚ÇÇ).obj K‚ÇÅ).obj K‚ÇÇ)`.

-/

assert_not_exists TwoSidedIdeal

open CategoryTheory Limits

variable {C‚ÇÅ C‚ÇÇ D : Type*} [Category C‚ÇÅ] [Category C‚ÇÇ] [Category D]

namespace CategoryTheory

namespace Functor

variable [HasZeroMorphisms C‚ÇÅ] [HasZeroMorphisms C‚ÇÇ] [HasZeroMorphisms D]
  (F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D) {I‚ÇÅ I‚ÇÇ J : Type*} (c‚ÇÅ : ComplexShape I‚ÇÅ) (c‚ÇÇ : ComplexShape I‚ÇÇ)
  [F.PreservesZeroMorphisms] [‚àÄ X‚ÇÅ, (F.obj X‚ÇÅ).PreservesZeroMorphisms]

variable {c‚ÇÅ} in
/-- Auxiliary definition for `mapBifunctorHomologicalComplex`. -/
@[simps!]
def mapBifunctorHomologicalComplexObj (K‚ÇÅ : HomologicalComplex C‚ÇÅ c‚ÇÅ) :
    HomologicalComplex C‚ÇÇ c‚ÇÇ ‚•§ HomologicalComplex‚ÇÇ D c‚ÇÅ c‚ÇÇ where
  obj K‚ÇÇ := HomologicalComplex‚ÇÇ.ofGradedObject c‚ÇÅ c‚ÇÇ
      (((GradedObject.mapBifunctor F I‚ÇÅ I‚ÇÇ).obj K‚ÇÅ.X).obj K‚ÇÇ.X)
      (fun i‚ÇÅ i‚ÇÅ' i‚ÇÇ => (F.map (K‚ÇÅ.d i‚ÇÅ i‚ÇÅ')).app (K‚ÇÇ.X i‚ÇÇ))
      (fun i‚ÇÅ i‚ÇÇ i‚ÇÇ' => (F.obj (K‚ÇÅ.X i‚ÇÅ)).map (K‚ÇÇ.d i‚ÇÇ i‚ÇÇ'))
      (fun i‚ÇÅ i‚ÇÅ' h‚ÇÅ i‚ÇÇ => by
        dsimp
        rw [K‚ÇÅ.shape _ _ h‚ÇÅ, Functor.map_zero, zero_app])
      (fun i‚ÇÅ i‚ÇÇ i‚ÇÇ' h‚ÇÇ => by
        dsimp
        rw [K‚ÇÇ.shape _ _ h‚ÇÇ, Functor.map_zero])
      (fun i‚ÇÅ i‚ÇÅ' i‚ÇÅ'' i‚ÇÇ => by
        dsimp
        rw [‚Üê NatTrans.comp_app, ‚Üê Functor.map_comp, HomologicalComplex.d_comp_d,
          Functor.map_zero, zero_app])
      (fun i‚ÇÅ i‚ÇÇ i‚ÇÇ' i‚ÇÇ'' => by
        dsimp
        rw [‚Üê Functor.map_comp, HomologicalComplex.d_comp_d, Functor.map_zero])
      (fun i‚ÇÅ i‚ÇÅ' i‚ÇÇ i‚ÇÇ' => by
        dsimp
        rw [NatTrans.naturality])
  map {K‚ÇÇ K‚ÇÇ' œÜ} := HomologicalComplex‚ÇÇ.homMk
      (((GradedObject.mapBifunctor F I‚ÇÅ I‚ÇÇ).obj K‚ÇÅ.X).map œÜ.f)
        (by dsimp; intros; rw [NatTrans.naturality]) (by
          dsimp
          intros
          simp only [‚Üê Functor.map_comp, œÜ.comm])
  map_id K‚ÇÇ := by dsimp; ext; dsimp; rw [Functor.map_id]
  map_comp f g := by dsimp; ext; dsimp; rw [Functor.map_comp]

/-- Given a functor `F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D`, this is the bifunctor which sends
`K‚ÇÅ : HomologicalComplex C‚ÇÅ c‚ÇÅ` and `K‚ÇÇ : HomologicalComplex C‚ÇÇ c‚ÇÇ` to the bicomplex
which is degree `(i‚ÇÅ, i‚ÇÇ)` consists of `(F.obj (K‚ÇÅ.X i‚ÇÅ)).obj (K‚ÇÇ.X i‚ÇÇ)`. -/
@[simps! obj_obj_X_X obj_obj_X_d obj_obj_d_f obj_map_f_f map_app_f_f]
def mapBifunctorHomologicalComplex :
    HomologicalComplex C‚ÇÅ c‚ÇÅ ‚•§ HomologicalComplex C‚ÇÇ c‚ÇÇ ‚•§ HomologicalComplex‚ÇÇ D c‚ÇÅ c‚ÇÇ where
  obj := mapBifunctorHomologicalComplexObj F c‚ÇÇ
  map {K‚ÇÅ K‚ÇÅ'} f :=
    { app := fun K‚ÇÇ => HomologicalComplex‚ÇÇ.homMk
        (((GradedObject.mapBifunctor F I‚ÇÅ I‚ÇÇ).map f.f).app K‚ÇÇ.X) (by
          intros
          dsimp
          simp only [‚Üê NatTrans.comp_app, ‚Üê F.map_comp, f.comm]) (by simp) }

variable {c‚ÇÅ c‚ÇÇ}

@[simp]
lemma mapBifunctorHomologicalComplex_obj_obj_toGradedObject
    (K‚ÇÅ : HomologicalComplex C‚ÇÅ c‚ÇÅ) (K‚ÇÇ : HomologicalComplex C‚ÇÇ c‚ÇÇ) :
    (((mapBifunctorHomologicalComplex F c‚ÇÅ c‚ÇÇ).obj K‚ÇÅ).obj K‚ÇÇ).toGradedObject =
      ((GradedObject.mapBifunctor F I‚ÇÅ I‚ÇÇ).obj K‚ÇÅ.X).obj K‚ÇÇ.X := rfl

end Functor

end CategoryTheory

namespace HomologicalComplex

variable {I‚ÇÅ I‚ÇÇ J : Type*} {c‚ÇÅ : ComplexShape I‚ÇÅ} {c‚ÇÇ : ComplexShape I‚ÇÇ}
  [HasZeroMorphisms C‚ÇÅ] [HasZeroMorphisms C‚ÇÇ] [Preadditive D]
  (K‚ÇÅ L‚ÇÅ : HomologicalComplex C‚ÇÅ c‚ÇÅ) (K‚ÇÇ L‚ÇÇ : HomologicalComplex C‚ÇÇ c‚ÇÇ)
  (f‚ÇÅ : K‚ÇÅ ‚ü∂ L‚ÇÅ) (f‚ÇÇ : K‚ÇÇ ‚ü∂ L‚ÇÇ)
  (F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D) [F.PreservesZeroMorphisms] [‚àÄ X‚ÇÅ, (F.obj X‚ÇÅ).PreservesZeroMorphisms]
  (c : ComplexShape J) [TotalComplexShape c‚ÇÅ c‚ÇÇ c]

/-- The condition that `((F.mapBifunctorHomologicalComplex c‚ÇÅ c‚ÇÇ).obj K‚ÇÅ).obj K‚ÇÇ` has
a total complex. -/
abbrev HasMapBifunctor := (((F.mapBifunctorHomologicalComplex c‚ÇÅ c‚ÇÇ).obj K‚ÇÅ).obj K‚ÇÇ).HasTotal c

variable [HasMapBifunctor K‚ÇÅ K‚ÇÇ F c] [HasMapBifunctor L‚ÇÅ L‚ÇÇ F c] [DecidableEq J]

/-- Given `K‚ÇÅ : HomologicalComplex C‚ÇÅ c‚ÇÅ`, `K‚ÇÇ : HomologicalComplex C‚ÇÇ c‚ÇÇ`,
a bifunctor `F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D` and a complex shape `ComplexShape J` such that we have
`[TotalComplexShape c‚ÇÅ c‚ÇÇ c]`, this `mapBifunctor K‚ÇÅ K‚ÇÇ F c : HomologicalComplex D c`
is the total complex of the bicomplex obtained by applying `F` to `K‚ÇÅ` and `K‚ÇÇ`. -/
noncomputable abbrev mapBifunctor : HomologicalComplex D c :=
  (((F.mapBifunctorHomologicalComplex c‚ÇÅ c‚ÇÇ).obj K‚ÇÅ).obj K‚ÇÇ).total c

/-- The inclusion of a summand of `(mapBifunctor K‚ÇÅ K‚ÇÇ F c).X j`. -/
noncomputable abbrev ŒπMapBifunctor
    (i‚ÇÅ : I‚ÇÅ) (i‚ÇÇ : I‚ÇÇ) (j : J) (h : ComplexShape.œÄ c‚ÇÅ c‚ÇÇ c (i‚ÇÅ, i‚ÇÇ) = j) :
    (F.obj (K‚ÇÅ.X i‚ÇÅ)).obj (K‚ÇÇ.X i‚ÇÇ) ‚ü∂ (mapBifunctor K‚ÇÅ K‚ÇÇ F c).X j :=
  (((F.mapBifunctorHomologicalComplex c‚ÇÅ c‚ÇÇ).obj K‚ÇÅ).obj K‚ÇÇ).ŒπTotal c i‚ÇÅ i‚ÇÇ j h

/-- The inclusion of a summand of `(mapBifunctor K‚ÇÅ K‚ÇÇ F c).X j`, or zero. -/
noncomputable abbrev ŒπMapBifunctorOrZero (i‚ÇÅ : I‚ÇÅ) (i‚ÇÇ : I‚ÇÇ) (j : J) :
    (F.obj (K‚ÇÅ.X i‚ÇÅ)).obj (K‚ÇÇ.X i‚ÇÇ) ‚ü∂ (mapBifunctor K‚ÇÅ K‚ÇÇ F c).X j :=
  (((F.mapBifunctorHomologicalComplex c‚ÇÅ c‚ÇÇ).obj K‚ÇÅ).obj K‚ÇÇ).ŒπTotalOrZero c i‚ÇÅ i‚ÇÇ j

lemma ŒπMapBifunctorOrZero_eq (i‚ÇÅ : I‚ÇÅ) (i‚ÇÇ : I‚ÇÇ) (j : J)
    (h : ComplexShape.œÄ c‚ÇÅ c‚ÇÇ c (i‚ÇÅ, i‚ÇÇ) = j) :
    ŒπMapBifunctorOrZero K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ j = ŒπMapBifunctor K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ j h := dif_pos h

lemma ŒπMapBifunctorOrZero_eq_zero (i‚ÇÅ : I‚ÇÅ) (i‚ÇÇ : I‚ÇÇ) (j : J)
    (h : ComplexShape.œÄ c‚ÇÅ c‚ÇÇ c (i‚ÇÅ, i‚ÇÇ) ‚â† j) :
    ŒπMapBifunctorOrZero K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ j = 0 := dif_neg h

section

variable {K‚ÇÅ K‚ÇÇ F c}
variable {A : D} {j : J}
  (f : ‚àÄ (i‚ÇÅ : I‚ÇÅ) (i‚ÇÇ : I‚ÇÇ) (_ : ComplexShape.œÄ c‚ÇÅ c‚ÇÇ c ‚ü®i‚ÇÅ, i‚ÇÇ‚ü© = j),
    (F.obj (K‚ÇÅ.X i‚ÇÅ)).obj (K‚ÇÇ.X i‚ÇÇ) ‚ü∂ A)

/-- Constructor for morphisms from `(mapBifunctor K‚ÇÅ K‚ÇÇ F c).X j`. -/
noncomputable def mapBifunctorDesc : (mapBifunctor K‚ÇÅ K‚ÇÇ F c).X j ‚ü∂ A :=
  HomologicalComplex‚ÇÇ.totalDesc _ f

@[reassoc (attr := simp)]
lemma Œπ_mapBifunctorDesc (i‚ÇÅ : I‚ÇÅ) (i‚ÇÇ : I‚ÇÇ) (h : ComplexShape.œÄ c‚ÇÅ c‚ÇÇ c ‚ü®i‚ÇÅ, i‚ÇÇ‚ü© = j) :
    ŒπMapBifunctor K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ j h ‚â´ mapBifunctorDesc f = f i‚ÇÅ i‚ÇÇ h := by
  apply HomologicalComplex‚ÇÇ.Œπ_totalDesc

end

namespace mapBifunctor

variable {K‚ÇÅ K‚ÇÇ F c} in
@[ext]
lemma hom_ext {Y : D} {j : J} {f g : (mapBifunctor K‚ÇÅ K‚ÇÇ F c).X j ‚ü∂ Y}
    (h : ‚àÄ (i‚ÇÅ : I‚ÇÅ) (i‚ÇÇ : I‚ÇÇ) (h : ComplexShape.œÄ c‚ÇÅ c‚ÇÇ c ‚ü®i‚ÇÅ, i‚ÇÇ‚ü© = j),
      ŒπMapBifunctor K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ j h ‚â´ f = ŒπMapBifunctor K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ j h ‚â´ g) :
    f = g :=
  HomologicalComplex‚ÇÇ.total.hom_ext _ h

section

variable (j j' : J)

/-- The first differential on `mapBifunctor K‚ÇÅ K‚ÇÇ F c` -/
noncomputable def D‚ÇÅ :
    (mapBifunctor K‚ÇÅ K‚ÇÇ F c).X j ‚ü∂ (mapBifunctor K‚ÇÅ K‚ÇÇ F c).X j' :=
  (((F.mapBifunctorHomologicalComplex c‚ÇÅ c‚ÇÇ).obj K‚ÇÅ).obj K‚ÇÇ).D‚ÇÅ c j j'

/-- The second differential on `mapBifunctor K‚ÇÅ K‚ÇÇ F c` -/
noncomputable def D‚ÇÇ :
    (mapBifunctor K‚ÇÅ K‚ÇÇ F c).X j ‚ü∂ (mapBifunctor K‚ÇÅ K‚ÇÇ F c).X j' :=
  (((F.mapBifunctorHomologicalComplex c‚ÇÅ c‚ÇÇ).obj K‚ÇÅ).obj K‚ÇÇ).D‚ÇÇ c j j'

lemma d_eq :
    (mapBifunctor K‚ÇÅ K‚ÇÇ F c).d j j' = D‚ÇÅ K‚ÇÅ K‚ÇÇ F c j j' + D‚ÇÇ K‚ÇÅ K‚ÇÇ F c j j' := rfl

end

section

variable (i‚ÇÅ : I‚ÇÅ) (i‚ÇÇ : I‚ÇÇ) (j : J)

/-- The first differential on a summand of `mapBifunctor K‚ÇÅ K‚ÇÇ F c` -/
noncomputable def d‚ÇÅ :
    (F.obj (K‚ÇÅ.X i‚ÇÅ)).obj (K‚ÇÇ.X i‚ÇÇ) ‚ü∂ (mapBifunctor K‚ÇÅ K‚ÇÇ F c).X j :=
  (((F.mapBifunctorHomologicalComplex c‚ÇÅ c‚ÇÇ).obj K‚ÇÅ).obj K‚ÇÇ).d‚ÇÅ c i‚ÇÅ i‚ÇÇ j

/-- The second differential on a summand of `mapBifunctor K‚ÇÅ K‚ÇÇ F c` -/
noncomputable def d‚ÇÇ :
    (F.obj (K‚ÇÅ.X i‚ÇÅ)).obj (K‚ÇÇ.X i‚ÇÇ) ‚ü∂ (mapBifunctor K‚ÇÅ K‚ÇÇ F c).X j :=
  (((F.mapBifunctorHomologicalComplex c‚ÇÅ c‚ÇÇ).obj K‚ÇÅ).obj K‚ÇÇ).d‚ÇÇ c i‚ÇÅ i‚ÇÇ j

lemma d‚ÇÅ_eq_zero (h : ¬¨ c‚ÇÅ.Rel i‚ÇÅ (c‚ÇÅ.next i‚ÇÅ)):
    d‚ÇÅ K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ j = 0 :=
  HomologicalComplex‚ÇÇ.d‚ÇÅ_eq_zero _ _ _ _ _ h

lemma d‚ÇÇ_eq_zero (h : ¬¨ c‚ÇÇ.Rel i‚ÇÇ (c‚ÇÇ.next i‚ÇÇ)):
    d‚ÇÇ K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ j = 0 :=
  HomologicalComplex‚ÇÇ.d‚ÇÇ_eq_zero _ _ _ _ _ h

lemma d‚ÇÅ_eq_zero' {i‚ÇÅ i‚ÇÅ' : I‚ÇÅ} (h : c‚ÇÅ.Rel i‚ÇÅ i‚ÇÅ') (i‚ÇÇ : I‚ÇÇ) (j : J)
    (h' : ComplexShape.œÄ c‚ÇÅ c‚ÇÇ c ‚ü®i‚ÇÅ', i‚ÇÇ‚ü© ‚â† j) :
    d‚ÇÅ K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ j = 0 :=
  HomologicalComplex‚ÇÇ.d‚ÇÅ_eq_zero' _ _ h _ _ h'

lemma d‚ÇÇ_eq_zero' (i‚ÇÅ : I‚ÇÅ) {i‚ÇÇ i‚ÇÇ' : I‚ÇÇ} (h : c‚ÇÇ.Rel i‚ÇÇ i‚ÇÇ') (j : J)
    (h' : ComplexShape.œÄ c‚ÇÅ c‚ÇÇ c ‚ü®i‚ÇÅ, i‚ÇÇ'‚ü© ‚â† j) :
    d‚ÇÇ K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ j = 0 :=
  HomologicalComplex‚ÇÇ.d‚ÇÇ_eq_zero' _ _ _ h _ h'

lemma d‚ÇÅ_eq' {i‚ÇÅ i‚ÇÅ' : I‚ÇÅ} (h : c‚ÇÅ.Rel i‚ÇÅ i‚ÇÅ') (i‚ÇÇ : I‚ÇÇ) (j : J) :
    d‚ÇÅ K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ j = ComplexShape.Œµ‚ÇÅ c‚ÇÅ c‚ÇÇ c ‚ü®i‚ÇÅ, i‚ÇÇ‚ü© ‚Ä¢
      ((F.map (K‚ÇÅ.d i‚ÇÅ i‚ÇÅ')).app (K‚ÇÇ.X i‚ÇÇ) ‚â´ ŒπMapBifunctorOrZero K‚ÇÅ K‚ÇÇ F c i‚ÇÅ' i‚ÇÇ j) :=
  HomologicalComplex‚ÇÇ.d‚ÇÅ_eq' _ _ h _ _

lemma d‚ÇÇ_eq' (i‚ÇÅ : I‚ÇÅ) {i‚ÇÇ i‚ÇÇ' : I‚ÇÇ} (h : c‚ÇÇ.Rel i‚ÇÇ i‚ÇÇ') (j : J) :
    d‚ÇÇ K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ j = ComplexShape.Œµ‚ÇÇ c‚ÇÅ c‚ÇÇ c ‚ü®i‚ÇÅ, i‚ÇÇ‚ü© ‚Ä¢
      ((F.obj (K‚ÇÅ.X i‚ÇÅ)).map (K‚ÇÇ.d i‚ÇÇ i‚ÇÇ') ‚â´ ŒπMapBifunctorOrZero K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ' j) :=
  HomologicalComplex‚ÇÇ.d‚ÇÇ_eq' _ _ _ h _

lemma d‚ÇÅ_eq {i‚ÇÅ i‚ÇÅ' : I‚ÇÅ} (h : c‚ÇÅ.Rel i‚ÇÅ i‚ÇÅ') (i‚ÇÇ : I‚ÇÇ) (j : J)
    (h' : ComplexShape.œÄ c‚ÇÅ c‚ÇÇ c ‚ü®i‚ÇÅ', i‚ÇÇ‚ü© = j) :
    d‚ÇÅ K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ j = ComplexShape.Œµ‚ÇÅ c‚ÇÅ c‚ÇÇ c ‚ü®i‚ÇÅ, i‚ÇÇ‚ü© ‚Ä¢
      ((F.map (K‚ÇÅ.d i‚ÇÅ i‚ÇÅ')).app (K‚ÇÇ.X i‚ÇÇ) ‚â´ ŒπMapBifunctor K‚ÇÅ K‚ÇÇ F c i‚ÇÅ' i‚ÇÇ j h') :=
  HomologicalComplex‚ÇÇ.d‚ÇÅ_eq _ _ h _ _ h'

lemma d‚ÇÇ_eq (i‚ÇÅ : I‚ÇÅ) {i‚ÇÇ i‚ÇÇ' : I‚ÇÇ} (h : c‚ÇÇ.Rel i‚ÇÇ i‚ÇÇ') (j : J)
    (h' : ComplexShape.œÄ c‚ÇÅ c‚ÇÇ c ‚ü®i‚ÇÅ, i‚ÇÇ'‚ü© = j) :
    d‚ÇÇ K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ j = ComplexShape.Œµ‚ÇÇ c‚ÇÅ c‚ÇÇ c ‚ü®i‚ÇÅ, i‚ÇÇ‚ü© ‚Ä¢
      ((F.obj (K‚ÇÅ.X i‚ÇÅ)).map (K‚ÇÇ.d i‚ÇÇ i‚ÇÇ') ‚â´ ŒπMapBifunctor K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ' j h') :=
  HomologicalComplex‚ÇÇ.d‚ÇÇ_eq _ _ _ h _ h'

end

section

variable (j j' : J) (i‚ÇÅ : I‚ÇÅ) (i‚ÇÇ : I‚ÇÇ) (h : ComplexShape.œÄ c‚ÇÅ c‚ÇÇ c (i‚ÇÅ, i‚ÇÇ) = j)

@[reassoc (attr := simp)]
lemma Œπ_D‚ÇÅ :
    ŒπMapBifunctor K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ j h ‚â´ D‚ÇÅ K‚ÇÅ K‚ÇÇ F c j j' = d‚ÇÅ K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ j' := by
  apply HomologicalComplex‚ÇÇ.Œπ_D‚ÇÅ

@[reassoc (attr := simp)]
lemma Œπ_D‚ÇÇ :
    ŒπMapBifunctor K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ j h ‚â´ D‚ÇÇ K‚ÇÅ K‚ÇÇ F c j j' = d‚ÇÇ K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ j' := by
  apply HomologicalComplex‚ÇÇ.Œπ_D‚ÇÇ

end

end mapBifunctor

section

variable {K‚ÇÅ K‚ÇÇ L‚ÇÅ L‚ÇÇ}

/-- The morphism `mapBifunctor K‚ÇÅ K‚ÇÇ F c ‚ü∂ mapBifunctor L‚ÇÅ L‚ÇÇ F c` induced by
morphisms of complexes `K‚ÇÅ ‚ü∂ L‚ÇÅ` and `K‚ÇÇ ‚ü∂ L‚ÇÇ`. -/
noncomputable def mapBifunctorMap : mapBifunctor K‚ÇÅ K‚ÇÇ F c ‚ü∂ mapBifunctor L‚ÇÅ L‚ÇÇ F c :=
  HomologicalComplex‚ÇÇ.total.map (((F.mapBifunctorHomologicalComplex c‚ÇÅ c‚ÇÇ).map f‚ÇÅ).app K‚ÇÇ ‚â´
    ((F.mapBifunctorHomologicalComplex c‚ÇÅ c‚ÇÇ).obj L‚ÇÅ).map f‚ÇÇ) c

@[reassoc (attr := simp)]
lemma Œπ_mapBifunctorMap (i‚ÇÅ : I‚ÇÅ) (i‚ÇÇ : I‚ÇÇ) (j : J)
    (h : ComplexShape.œÄ c‚ÇÅ c‚ÇÇ c (i‚ÇÅ, i‚ÇÇ) = j) :
    ŒπMapBifunctor K‚ÇÅ K‚ÇÇ F c i‚ÇÅ i‚ÇÇ j h ‚â´ (mapBifunctorMap f‚ÇÅ f‚ÇÇ F c).f j =
      (F.map (f‚ÇÅ.f i‚ÇÅ)).app (K‚ÇÇ.X i‚ÇÇ) ‚â´ (F.obj (L‚ÇÅ.X i‚ÇÅ)).map (f‚ÇÇ.f i‚ÇÇ) ‚â´
        ŒπMapBifunctor L‚ÇÅ L‚ÇÇ F c i‚ÇÅ i‚ÇÇ j h := by
  simp [mapBifunctorMap]

end

@[simp]
lemma mapBifunctorMap_map_add‚ÇÅ {C‚ÇÅ C‚ÇÇ : Type*} [Category C‚ÇÅ] [Category C‚ÇÇ]
    [Preadditive C‚ÇÅ] [HasZeroMorphisms C‚ÇÇ]
    {I‚ÇÅ I‚ÇÇ : Type*} {c‚ÇÅ : ComplexShape I‚ÇÅ} {c‚ÇÇ : ComplexShape I‚ÇÇ}
    {K‚ÇÅ L‚ÇÅ : HomologicalComplex C‚ÇÅ c‚ÇÅ} (f g : K‚ÇÅ ‚ü∂ L‚ÇÅ) {K‚ÇÇ L‚ÇÇ : HomologicalComplex C‚ÇÇ c‚ÇÇ}
    (h : K‚ÇÇ ‚ü∂ L‚ÇÇ) (F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D) [F.Additive]
    [‚àÄ X‚ÇÅ, (F.obj X‚ÇÅ).PreservesZeroMorphisms]
    {J : Type*} [DecidableEq J] (c : ComplexShape J) [TotalComplexShape c‚ÇÅ c‚ÇÇ c]
    [HasMapBifunctor K‚ÇÅ K‚ÇÇ F c] [HasMapBifunctor L‚ÇÅ L‚ÇÇ F c] :
    mapBifunctorMap (f + g) h F c = mapBifunctorMap f h F c + mapBifunctorMap g h F c := by
  aesop_cat

@[simp]
lemma mapBifunctorMap_map_add‚ÇÇ {C‚ÇÅ C‚ÇÇ : Type*} [Category C‚ÇÅ] [Category C‚ÇÇ]
    [HasZeroMorphisms C‚ÇÅ] [Preadditive C‚ÇÇ]
    {I‚ÇÅ I‚ÇÇ : Type*} {c‚ÇÅ : ComplexShape I‚ÇÅ} {c‚ÇÇ : ComplexShape I‚ÇÇ}
    {K‚ÇÅ L‚ÇÅ : HomologicalComplex C‚ÇÅ c‚ÇÅ} (f : K‚ÇÅ ‚ü∂ L‚ÇÅ) {K‚ÇÇ L‚ÇÇ : HomologicalComplex C‚ÇÇ c‚ÇÇ}
    (g h : K‚ÇÇ ‚ü∂ L‚ÇÇ) (F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D) [F.PreservesZeroMorphisms]
    [‚àÄ X‚ÇÅ, (F.obj X‚ÇÅ).Additive]
    {J : Type*} [DecidableEq J] (c : ComplexShape J) [TotalComplexShape c‚ÇÅ c‚ÇÇ c]
    [HasMapBifunctor K‚ÇÅ K‚ÇÇ F c] [HasMapBifunctor L‚ÇÅ L‚ÇÇ F c] :
    mapBifunctorMap f (g + h) F c = mapBifunctorMap f g F c + mapBifunctorMap f h F c := by
  aesop_cat

end HomologicalComplex

namespace CategoryTheory.Functor

open HomologicalComplex

section

variable [HasZeroMorphisms C‚ÇÅ] [HasZeroMorphisms C‚ÇÇ] [Preadditive D]
  (F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D) [F.PreservesZeroMorphisms] [‚àÄ X‚ÇÅ, (F.obj X‚ÇÅ).PreservesZeroMorphisms]
  {I‚ÇÅ I‚ÇÇ J : Type*} (c‚ÇÅ : ComplexShape I‚ÇÅ) (c‚ÇÇ : ComplexShape I‚ÇÇ)
  (c : ComplexShape J) [TotalComplexShape c‚ÇÅ c‚ÇÇ c] [DecidableEq J]
  [‚àÄ (K‚ÇÅ : HomologicalComplex C‚ÇÅ c‚ÇÅ) (K‚ÇÇ : HomologicalComplex C‚ÇÇ c‚ÇÇ), HasMapBifunctor K‚ÇÅ K‚ÇÇ F c]

@[simps]
noncomputable def bifunctorMapHomologicalComplex :
    HomologicalComplex C‚ÇÅ c‚ÇÅ ‚•§ HomologicalComplex C‚ÇÇ c‚ÇÇ ‚•§ HomologicalComplex D c where
  obj K‚ÇÅ :=
    { obj K‚ÇÇ := mapBifunctor K‚ÇÅ K‚ÇÇ F c
      map f‚ÇÇ := mapBifunctorMap (ùüô K‚ÇÅ) f‚ÇÇ F c
      map_id _ := by ext; dsimp; simp
      map_comp _ _ := by ext; dsimp; simp }
  map f‚ÇÅ :=
    { app K‚ÇÇ := mapBifunctorMap f‚ÇÅ (ùüô K‚ÇÇ) F c
      naturality _ _ _ := by ext; dsimp; simp }
  map_id _ := by ext; dsimp; simp
  map_comp _ _ := by ext; dsimp; simp

end

section

variable [Preadditive C‚ÇÅ] [HasZeroMorphisms C‚ÇÇ] [Preadditive D]
  (F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D) [F.Additive] [‚àÄ X‚ÇÅ, (F.obj X‚ÇÅ).PreservesZeroMorphisms]
  {I‚ÇÅ I‚ÇÇ J : Type*} (c‚ÇÅ : ComplexShape I‚ÇÅ) (c‚ÇÇ : ComplexShape I‚ÇÇ)
  (c : ComplexShape J) [TotalComplexShape c‚ÇÅ c‚ÇÇ c] [DecidableEq J]
  [‚àÄ (K‚ÇÅ : HomologicalComplex C‚ÇÅ c‚ÇÅ) (K‚ÇÇ : HomologicalComplex C‚ÇÇ c‚ÇÇ), HasMapBifunctor K‚ÇÅ K‚ÇÇ F c]

instance : (bifunctorMapHomologicalComplex F c‚ÇÅ c‚ÇÇ c).Additive where

instance (K‚ÇÇ : HomologicalComplex C‚ÇÇ c‚ÇÇ) :
    ((bifunctorMapHomologicalComplex F c‚ÇÅ c‚ÇÇ c).flip.obj K‚ÇÇ).Additive where

end

section

variable [HasZeroMorphisms C‚ÇÅ] [Preadditive C‚ÇÇ] [Preadditive D]
  (F : C‚ÇÅ ‚•§ C‚ÇÇ ‚•§ D) [F.PreservesZeroMorphisms] [‚àÄ X‚ÇÅ, (F.obj X‚ÇÅ).Additive]
  {I‚ÇÅ I‚ÇÇ J : Type*} (c‚ÇÅ : ComplexShape I‚ÇÅ) (c‚ÇÇ : ComplexShape I‚ÇÇ)
  (c : ComplexShape J) [TotalComplexShape c‚ÇÅ c‚ÇÇ c] [DecidableEq J]
  [‚àÄ (K‚ÇÅ : HomologicalComplex C‚ÇÅ c‚ÇÅ) (K‚ÇÇ : HomologicalComplex C‚ÇÇ c‚ÇÇ), HasMapBifunctor K‚ÇÅ K‚ÇÇ F c]

instance : (bifunctorMapHomologicalComplex F c‚ÇÅ c‚ÇÇ c).flip.Additive where

instance (K‚ÇÅ : HomologicalComplex C‚ÇÅ c‚ÇÅ) :
    ((bifunctorMapHomologicalComplex F c‚ÇÅ c‚ÇÇ c).obj K‚ÇÅ).Additive where

end

end CategoryTheory.Functor
