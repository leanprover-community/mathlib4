/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.Homotopy
import Mathlib.Algebra.Homology.HomotopyCategory.Shift
import Mathlib.Algebra.GroupPower.NegOnePow
import Mathlib.Algebra.Category.GroupCat.Preadditive
import Mathlib.Tactic.Linarith
import Mathlib.Tactic.Ring
import Mathlib.CategoryTheory.Linear.LinearFunctor

/-! The cochain complex of homomorphisms between cochain complexes

If `F` and `G` are cochain complexes (indexed by `â„¤`) in a preadditive category,
there is a cochain complex of abelian groups whose `0`-cocycles identify to
morphisms `F âŸ¶ G`. Informally, in degree `n`, this complex shall consist of
cochains of degree `n` from `F` to `G`, i.e. arbitrary families for morphisms
`F.X p âŸ¶ G.X (p + n)`. This complex shall be denoted `HomComplex F G`.

In order to avoid type theoretic issues, a cochain of degree `n : â„¤`
(i.e. a term of type of `Cochain F G n`) shall be defined here
as the data of a morphism `F.X p âŸ¶ G.X q` for all triplets
`âŸ¨p, q, hpqâŸ©` where `p` and `q` are integers and `hpq : p + n = q`.
If `Î± : Cochain F G n`, we shall define `Î±.v p q hpq : F.X p âŸ¶ G.X q`.

We follow the signs conventions appearing in the introduction of
[Brian Conrad's book *Grothendieck duality and base change*][conrad2000].

TODO:
* Behaviour with respect to shifting the cochain complexes `F` and `G`.

## References
* [Brian Conrad, Grothendieck duality and base change][conrad2000]

-/

open CategoryTheory Category Limits Preadditive

universe v u

variable {C : Type u} [Category.{v} C] [Preadditive C] {R : Type*} [Ring R] [Linear R C]

namespace CochainComplex

variable {F G K L M : CochainComplex C â„¤} (n m : â„¤)

namespace HomComplex

/-- A term of type `HomComplex.Triplet n` consists of two integers `p` and `q`
such that `p + n = q`. (This type is introduced so that the instance
`AddCommGroup (Cochain F G n)` defined below can be found automatically.) -/
structure Triplet (n : â„¤) where
  /-- a first integer -/
  p : â„¤
  /-- a second integer -/
  q : â„¤
  /-- the condition on the two integers -/
  hpq : p + n = q

variable (F G)

/-- A cochain of degree `n : â„¤` between to cochain complexes `F` and `G` consists
of a family of morphisms `F.X p âŸ¶ G.X q` whenever `p + n = q`, i.e. for all
triplets in `HomComplex.Triplet n`. -/
def Cochain := âˆ€ (T : Triplet n), F.X T.p âŸ¶ G.X T.q

instance : AddCommGroup (Cochain F G n) := by
  dsimp only [Cochain]
  infer_instance

instance : Module R (Cochain F G n) := by
  dsimp only [Cochain]
  infer_instance

namespace Cochain

variable {F G n}

/-- A practical constructor for cochains. -/
def mk (v : âˆ€ (p q : â„¤) (_ : p + n = q), F.X p âŸ¶ G.X q) : Cochain F G n :=
  fun âŸ¨p, q, hpqâŸ© => v p q hpq

/-- The value of a cochain on a triplet `âŸ¨p, q, hpqâŸ©`. -/
@[pp_dot]
def v (Î³ : Cochain F G n) (p q : â„¤) (hpq : p + n = q) :
    F.X p âŸ¶ G.X q := Î³ âŸ¨p, q, hpqâŸ©

@[simp]
lemma mk_v (v : âˆ€ (p q : â„¤) (_ : p + n = q), F.X p âŸ¶ G.X q) (p q : â„¤) (hpq : p + n = q) :
    (Cochain.mk v).v p q hpq = v p q hpq := rfl

lemma congr_v {zâ‚ zâ‚‚ : Cochain F G n} (h : zâ‚ = zâ‚‚) (p q : â„¤) (hpq : p + n = q) :
    zâ‚.v p q hpq = zâ‚‚.v p q hpq := by subst h; rfl

@[ext]
lemma ext (zâ‚ zâ‚‚ : Cochain F G n)
    (h : âˆ€ (p q hpq), zâ‚.v p q hpq = zâ‚‚.v p q hpq) : zâ‚ = zâ‚‚ := by
  funext âŸ¨p, q, hpqâŸ©
  apply h

@[ext 1100]
lemma extâ‚€ (zâ‚ zâ‚‚ : Cochain F G 0)
    (h : âˆ€ (p : â„¤), zâ‚.v p p (add_zero p) = zâ‚‚.v p p (add_zero p)) : zâ‚ = zâ‚‚ := by
  ext p q hpq
  obtain rfl : q = p := by rw [â† hpq, add_zero]
  exact h q

@[simp]
lemma zero_v {n : â„¤} (p q : â„¤) (hpq : p + n = q) :
    (0 : Cochain F G n).v p q hpq = 0 := rfl

@[simp]
lemma add_v {n : â„¤} (zâ‚ zâ‚‚ : Cochain F G n) (p q : â„¤) (hpq : p + n = q) :
    (zâ‚ + zâ‚‚).v p q hpq = zâ‚.v p q hpq + zâ‚‚.v p q hpq := rfl

@[simp]
lemma sub_v {n : â„¤} (zâ‚ zâ‚‚ : Cochain F G n) (p q : â„¤) (hpq : p + n = q) :
    (zâ‚ - zâ‚‚).v p q hpq = zâ‚.v p q hpq - zâ‚‚.v p q hpq := rfl

@[simp]
lemma neg_v {n : â„¤} (z : Cochain F G n) (p q : â„¤) (hpq : p + n = q) :
    (-z).v p q hpq = - (z.v p q hpq) := rfl

@[simp]
lemma smul_v {n : â„¤} (k : R) (z : Cochain F G n) (p q : â„¤) (hpq : p + n = q) :
    (k â€¢ z).v p q hpq = k â€¢ (z.v p q hpq) := rfl

@[simp]
lemma units_smul_v {n : â„¤} (k : RË£) (z : Cochain F G n) (p q : â„¤) (hpq : p + n = q) :
    (k â€¢ z).v p q hpq = k â€¢ (z.v p q hpq) := rfl

/-- A cochain of degree `0` from `F` to `G` can be constructed from a family
of morphisms `F.X p âŸ¶ G.X p` for all `p : â„¤`. -/
def ofHoms (Ïˆ : âˆ€ (p : â„¤), F.X p âŸ¶ G.X p) : Cochain F G 0 :=
  Cochain.mk (fun p q hpq => Ïˆ p â‰« eqToHom (by rw [â† hpq, add_zero]))

@[simp]
lemma ofHoms_v (Ïˆ : âˆ€ (p : â„¤), F.X p âŸ¶ G.X p) (p : â„¤) :
    (ofHoms Ïˆ).v p p (add_zero p) = Ïˆ p := by
  simp only [ofHoms, mk_v, eqToHom_refl, comp_id]

@[simp]
lemma ofHoms_zero : ofHoms (fun p => (0 : F.X p âŸ¶ G.X p)) = 0 := by aesop_cat

@[simp]
lemma ofHoms_v_comp_d (Ïˆ : âˆ€ (p : â„¤), F.X p âŸ¶ G.X p) (p q q' : â„¤) (hpq : p + 0 = q) :
    (ofHoms Ïˆ).v p q hpq â‰« G.d q q' = Ïˆ p â‰« G.d p q' := by
  rw [add_zero] at hpq
  subst hpq
  rw [ofHoms_v]

@[simp]
lemma d_comp_ofHoms_v (Ïˆ : âˆ€ (p : â„¤), F.X p âŸ¶ G.X p) (p' p q : â„¤) (hpq : p + 0 = q) :
    F.d p' p â‰« (ofHoms Ïˆ).v p q hpq = F.d p' q â‰« Ïˆ q := by
  rw [add_zero] at hpq
  subst hpq
  rw [ofHoms_v]

/-- The `0`-cochain attached to a morphism of cochain complexes. -/
def ofHom (Ï† : F âŸ¶ G) : Cochain F G 0 := ofHoms (fun p => Ï†.f p)

variable (F G)

@[simp]
lemma ofHom_zero : ofHom (0 : F âŸ¶ G) = 0 := by
  simp only [ofHom, HomologicalComplex.zero_f_apply, ofHoms_zero]

variable {F G}

@[simp]
lemma ofHom_v (Ï† : F âŸ¶ G) (p : â„¤) : (ofHom Ï†).v p p (add_zero p) = Ï†.f p := by
  simp only [ofHom, ofHoms_v]

@[simp]
lemma ofHom_v_comp_d (Ï† : F âŸ¶ G) (p q q' : â„¤) (hpq : p + 0 = q) :
    (ofHom Ï†).v p q hpq â‰« G.d q q' = Ï†.f p â‰« G.d p q' :=
by simp only [ofHom, ofHoms_v_comp_d]

@[simp]
lemma d_comp_ofHom_v (Ï† : F âŸ¶ G) (p' p q : â„¤) (hpq : p + 0 = q) :
    F.d p' p â‰« (ofHom Ï†).v p q hpq = F.d p' q â‰« Ï†.f q := by
  simp only [ofHom, d_comp_ofHoms_v]

@[simp]
lemma ofHom_add (Ï†â‚ Ï†â‚‚ : F âŸ¶ G) :
    Cochain.ofHom (Ï†â‚ + Ï†â‚‚) = Cochain.ofHom Ï†â‚ + Cochain.ofHom Ï†â‚‚ := by aesop_cat

@[simp]
lemma ofHom_sub (Ï†â‚ Ï†â‚‚ : F âŸ¶ G) :
    Cochain.ofHom (Ï†â‚ - Ï†â‚‚) = Cochain.ofHom Ï†â‚ - Cochain.ofHom Ï†â‚‚ := by aesop_cat

@[simp]
lemma ofHom_neg (Ï† : F âŸ¶ G) :
    Cochain.ofHom (-Ï†) = -Cochain.ofHom Ï† := by aesop_cat

/-- The cochain of degree `-1` given by an homotopy between two morphism of complexes. -/
def ofHomotopy {Ï†â‚ Ï†â‚‚ : F âŸ¶ G} (ho : Homotopy Ï†â‚ Ï†â‚‚) : Cochain F G (-1) :=
  Cochain.mk (fun p q _ => ho.hom p q)

@[simp]
lemma ofHomotopy_ofEq {Ï†â‚ Ï†â‚‚ : F âŸ¶ G} (h : Ï†â‚ = Ï†â‚‚) :
    ofHomotopy (Homotopy.ofEq h) = 0 := rfl

@[simp]
lemma ofHomotopy_refl (Ï† : F âŸ¶ G) :
    ofHomotopy (Homotopy.refl Ï†) = 0 := rfl

@[reassoc]
lemma v_comp_XIsoOfEq_hom
    (Î³ : Cochain F G n) (p q q' : â„¤) (hpq : p + n = q) (hq' : q = q') :
    Î³.v p q hpq â‰« (HomologicalComplex.XIsoOfEq G hq').hom = Î³.v p q' (by rw [â† hq', hpq]) := by
  subst hq'
  simp only [HomologicalComplex.XIsoOfEq, eqToIso_refl, Iso.refl_hom, comp_id]

@[reassoc]
lemma v_comp_XIsoOfEq_inv
    (Î³ : Cochain F G n) (p q q' : â„¤) (hpq : p + n = q) (hq' : q' = q) :
    Î³.v p q hpq â‰« (HomologicalComplex.XIsoOfEq G hq').inv = Î³.v p q' (by rw [hq', hpq]) := by
  subst hq'
  simp only [HomologicalComplex.XIsoOfEq, eqToIso_refl, Iso.refl_inv, comp_id]

@[reassoc]
lemma XIsoOfEq_hom_comp_v
    (Î³ : Cochain F G n) (p p' q : â„¤) (hpq' : p' + n = q) (hp' : p = p') :
    (HomologicalComplex.XIsoOfEq F hp').hom â‰« Î³.v p' q hpq' = Î³.v p q (by rw [hp', hpq']) := by
  subst hp'
  simp only [HomologicalComplex.XIsoOfEq_rfl, Iso.refl_hom, id_comp]

@[reassoc]
lemma XIsoOfEq_inv_comp_v
    (Î³ : Cochain F G n) (p p' q : â„¤) (hpq' : p' + n = q) (hp' : p' = p) :
    (HomologicalComplex.XIsoOfEq F hp').inv â‰« Î³.v p' q hpq' = Î³.v p q (by rw [â† hp', hpq']) := by
  subst hp'
  simp only [HomologicalComplex.XIsoOfEq_rfl, Iso.refl_inv, id_comp]

/-- The composition of cochains. -/
@[pp_dot]
def comp {nâ‚ nâ‚‚ nâ‚â‚‚ : â„¤} (zâ‚ : Cochain F G nâ‚) (zâ‚‚ : Cochain G K nâ‚‚) (h : nâ‚ + nâ‚‚ = nâ‚â‚‚) :
    Cochain F K nâ‚â‚‚ :=
  Cochain.mk (fun p q hpq => zâ‚.v p (p + nâ‚) rfl â‰« zâ‚‚.v (p + nâ‚) q (by linarith))

/-! If `zâ‚` is a cochain of degree `nâ‚` and `zâ‚‚` is a cochain of degree `nâ‚‚`, and that
we have a relation `h : nâ‚ + nâ‚‚ = nâ‚â‚‚`, then `zâ‚.comp zâ‚‚ h` is a cochain of degree `nâ‚â‚‚`.
The following lemma `comp_v` computes the value of this composition `zâ‚.comp zâ‚‚ h`
on a triplet `âŸ¨pâ‚, pâ‚ƒ, _âŸ©` (with `pâ‚ + nâ‚â‚‚ = pâ‚ƒ`). In order to use this lemma,
we need to provide an intermediate integer `pâ‚‚` such that `pâ‚ + nâ‚ = pâ‚‚`.
It is advisable to use a `pâ‚‚` that has good definitional properties
(i.e. `pâ‚ + nâ‚` is not always the best choice.)

When `zâ‚` or `zâ‚‚` is a `0`-cochain, there is a better choice of `pâ‚‚`, and this leads
to the two simplification lemmas `comp_zero_cochain_v` and `zero_cochain_comp_v`.

-/

lemma comp_v {nâ‚ nâ‚‚ nâ‚â‚‚ : â„¤} (zâ‚ : Cochain F G nâ‚) (zâ‚‚ : Cochain G K nâ‚‚) (h : nâ‚ + nâ‚‚ = nâ‚â‚‚)
    (pâ‚ pâ‚‚ pâ‚ƒ : â„¤) (hâ‚ : pâ‚ + nâ‚ = pâ‚‚) (hâ‚‚ : pâ‚‚ + nâ‚‚ = pâ‚ƒ) :
    (zâ‚.comp zâ‚‚ h).v pâ‚ pâ‚ƒ (by rw [â† hâ‚‚, â† hâ‚, â† h, add_assoc]) =
      zâ‚.v pâ‚ pâ‚‚ hâ‚ â‰« zâ‚‚.v pâ‚‚ pâ‚ƒ hâ‚‚ := by
  subst hâ‚; rfl

@[simp]
lemma comp_zero_cochain_v (zâ‚ : Cochain F G n) (zâ‚‚ : Cochain G K 0) (p q : â„¤) (hpq : p + n = q) :
    (zâ‚.comp zâ‚‚ (add_zero n)).v p q hpq = zâ‚.v p q hpq â‰« zâ‚‚.v q q (add_zero q) :=
  comp_v zâ‚ zâ‚‚ (add_zero n) p q q hpq (add_zero q)

@[simp]
lemma zero_cochain_comp_v (zâ‚ : Cochain F G 0) (zâ‚‚ : Cochain G K n) (p q : â„¤) (hpq : p + n = q) :
    (zâ‚.comp zâ‚‚ (zero_add n)).v p q hpq = zâ‚.v p p (add_zero p) â‰« zâ‚‚.v p q hpq :=
  comp_v zâ‚ zâ‚‚ (zero_add n) p p q (add_zero p) hpq

/-- The associativity of the composition of cochains. -/
lemma comp_assoc {nâ‚ nâ‚‚ nâ‚ƒ nâ‚â‚‚ nâ‚‚â‚ƒ nâ‚â‚‚â‚ƒ : â„¤}
    (zâ‚ : Cochain F G nâ‚) (zâ‚‚ : Cochain G K nâ‚‚) (zâ‚ƒ : Cochain K L nâ‚ƒ)
    (hâ‚â‚‚ : nâ‚ + nâ‚‚ = nâ‚â‚‚) (hâ‚‚â‚ƒ : nâ‚‚ + nâ‚ƒ = nâ‚‚â‚ƒ) (hâ‚â‚‚â‚ƒ : nâ‚ + nâ‚‚ + nâ‚ƒ = nâ‚â‚‚â‚ƒ) :
    (zâ‚.comp zâ‚‚ hâ‚â‚‚).comp zâ‚ƒ (show nâ‚â‚‚ + nâ‚ƒ = nâ‚â‚‚â‚ƒ by rw [â† hâ‚â‚‚, hâ‚â‚‚â‚ƒ]) =
      zâ‚.comp (zâ‚‚.comp zâ‚ƒ hâ‚‚â‚ƒ) (by rw [â† hâ‚‚â‚ƒ, â† hâ‚â‚‚â‚ƒ, add_assoc]) := by
  substs hâ‚â‚‚ hâ‚‚â‚ƒ hâ‚â‚‚â‚ƒ
  ext p q hpq
  rw [comp_v _ _ rfl p (p + nâ‚ + nâ‚‚) q (by linarith) (by linarith),
    comp_v zâ‚ zâ‚‚ rfl p (p + nâ‚) (p + nâ‚ + nâ‚‚) (by linarith) (by linarith),
    comp_v zâ‚ (zâ‚‚.comp zâ‚ƒ rfl) (add_assoc nâ‚ nâ‚‚ nâ‚ƒ).symm p (p + nâ‚) q (by linarith) (by linarith),
    comp_v zâ‚‚ zâ‚ƒ rfl (p + nâ‚) (p + nâ‚ + nâ‚‚) q (by linarith) (by linarith), assoc]

/-! The formulation of the associativity of the composition of cochains given by the
lemma `comp_assoc` often requires a careful selection of degrees with good definitional
properties. In a few cases, like when one of the three cochains is a `0`-cochain,
there are better choices, which provides the following simplification lemmas. -/

@[simp]
lemma comp_assoc_of_first_is_zero_cochain {nâ‚‚ nâ‚ƒ nâ‚‚â‚ƒ : â„¤}
    (zâ‚ : Cochain F G 0) (zâ‚‚ : Cochain G K nâ‚‚) (zâ‚ƒ : Cochain K L nâ‚ƒ)
    (hâ‚‚â‚ƒ : nâ‚‚ + nâ‚ƒ = nâ‚‚â‚ƒ) :
    (zâ‚.comp zâ‚‚ (zero_add nâ‚‚)).comp zâ‚ƒ hâ‚‚â‚ƒ = zâ‚.comp (zâ‚‚.comp zâ‚ƒ hâ‚‚â‚ƒ) (zero_add nâ‚‚â‚ƒ) :=
  comp_assoc _ _ _ _ _ (by linarith)

@[simp]
lemma comp_assoc_of_second_is_zero_cochain {nâ‚ nâ‚ƒ nâ‚â‚ƒ : â„¤}
    (zâ‚ : Cochain F G nâ‚) (zâ‚‚ : Cochain G K 0) (zâ‚ƒ : Cochain K L nâ‚ƒ) (hâ‚â‚ƒ : nâ‚ + nâ‚ƒ = nâ‚â‚ƒ) :
    (zâ‚.comp zâ‚‚ (add_zero nâ‚)).comp zâ‚ƒ hâ‚â‚ƒ = zâ‚.comp (zâ‚‚.comp zâ‚ƒ (zero_add nâ‚ƒ)) hâ‚â‚ƒ :=
  comp_assoc _ _ _ _ _ (by linarith)

@[simp]
lemma comp_assoc_of_third_is_zero_cochain {nâ‚ nâ‚‚ nâ‚â‚‚ : â„¤}
    (zâ‚ : Cochain F G nâ‚) (zâ‚‚ : Cochain G K nâ‚‚) (zâ‚ƒ : Cochain K L 0) (hâ‚â‚‚ : nâ‚ + nâ‚‚ = nâ‚â‚‚) :
    (zâ‚.comp zâ‚‚ hâ‚â‚‚).comp zâ‚ƒ (add_zero nâ‚â‚‚) = zâ‚.comp (zâ‚‚.comp zâ‚ƒ (add_zero nâ‚‚)) hâ‚â‚‚ :=
  comp_assoc _ _ _ _ _ (by linarith)

@[simp]
lemma comp_assoc_of_second_degree_eq_neg_third_degree {nâ‚ nâ‚‚ nâ‚â‚‚ : â„¤}
    (zâ‚ : Cochain F G nâ‚) (zâ‚‚ : Cochain G K (-nâ‚‚)) (zâ‚ƒ : Cochain K L nâ‚‚) (hâ‚â‚‚ : nâ‚ + (-nâ‚‚) = nâ‚â‚‚) :
    (zâ‚.comp zâ‚‚ hâ‚â‚‚).comp zâ‚ƒ
      (show nâ‚â‚‚ + nâ‚‚ = nâ‚ by rw [â† hâ‚â‚‚, add_assoc, neg_add_self, add_zero]) =
      zâ‚.comp (zâ‚‚.comp zâ‚ƒ (neg_add_self nâ‚‚)) (add_zero nâ‚) :=
  comp_assoc _ _ _ _ _ (by linarith)

@[simp]
lemma comp_assoc_of_third_degree_eq_neg_second_degree {nâ‚ nâ‚‚ nâ‚â‚‚ : â„¤}
    (zâ‚ : Cochain F G nâ‚) (zâ‚‚ : Cochain G K nâ‚‚) (zâ‚ƒ : Cochain K L (-nâ‚‚)) (hâ‚â‚‚ : nâ‚ + nâ‚‚ = nâ‚â‚‚) :
    (zâ‚.comp zâ‚‚ hâ‚â‚‚).comp zâ‚ƒ
      (show nâ‚â‚‚ + (-nâ‚‚) = nâ‚ by rw [â† hâ‚â‚‚, add_neg_cancel_right]) =
      zâ‚.comp (zâ‚‚.comp zâ‚ƒ (add_neg_self nâ‚‚)) (add_zero nâ‚) :=
  comp_assoc _ _ _ _ _ (by linarith)

@[simp]
protected lemma zero_comp {nâ‚ nâ‚‚ nâ‚â‚‚ : â„¤} (zâ‚‚ : Cochain G K nâ‚‚)
    (h : nâ‚ + nâ‚‚ = nâ‚â‚‚) : (0 : Cochain F G nâ‚).comp zâ‚‚ h = 0 := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by linarith), zero_v, zero_comp]

@[simp]
protected lemma add_comp {nâ‚ nâ‚‚ nâ‚â‚‚ : â„¤} (zâ‚ zâ‚' : Cochain F G nâ‚) (zâ‚‚ : Cochain G K nâ‚‚)
    (h : nâ‚ + nâ‚‚ = nâ‚â‚‚) : (zâ‚ + zâ‚').comp zâ‚‚ h = zâ‚.comp zâ‚‚ h + zâ‚'.comp zâ‚‚ h := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by linarith), add_v, add_comp]

@[simp]
protected lemma sub_comp {nâ‚ nâ‚‚ nâ‚â‚‚ : â„¤} (zâ‚ zâ‚' : Cochain F G nâ‚) (zâ‚‚ : Cochain G K nâ‚‚)
    (h : nâ‚ + nâ‚‚ = nâ‚â‚‚) : (zâ‚ - zâ‚').comp zâ‚‚ h = zâ‚.comp zâ‚‚ h - zâ‚'.comp zâ‚‚ h := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by linarith), sub_v, sub_comp]

@[simp]
protected lemma neg_comp {nâ‚ nâ‚‚ nâ‚â‚‚ : â„¤} (zâ‚ : Cochain F G nâ‚) (zâ‚‚ : Cochain G K nâ‚‚)
    (h : nâ‚ + nâ‚‚ = nâ‚â‚‚) : (-zâ‚).comp zâ‚‚ h = -zâ‚.comp zâ‚‚ h := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by linarith), neg_v, neg_comp]

@[simp]
protected lemma smul_comp {nâ‚ nâ‚‚ nâ‚â‚‚ : â„¤} (k : R) (zâ‚ : Cochain F G nâ‚) (zâ‚‚ : Cochain G K nâ‚‚)
    (h : nâ‚ + nâ‚‚ = nâ‚â‚‚) : (k â€¢ zâ‚).comp zâ‚‚ h = k â€¢ (zâ‚.comp zâ‚‚ h) := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by linarith), smul_v, Linear.smul_comp]

@[simp]
lemma units_smul_comp {nâ‚ nâ‚‚ nâ‚â‚‚ : â„¤} (k : RË£) (zâ‚ : Cochain F G nâ‚) (zâ‚‚ : Cochain G K nâ‚‚)
    (h : nâ‚ + nâ‚‚ = nâ‚â‚‚) : (k â€¢ zâ‚).comp zâ‚‚ h = k â€¢ (zâ‚.comp zâ‚‚ h) := by
  apply Cochain.smul_comp

@[simp]
protected lemma id_comp {n : â„¤} (zâ‚‚ : Cochain F G n) :
    (Cochain.ofHom (ðŸ™ F)).comp zâ‚‚ (zero_add n) = zâ‚‚ := by
  ext p q hpq
  simp only [zero_cochain_comp_v, ofHom_v, HomologicalComplex.id_f, id_comp]

@[simp]
protected lemma comp_zero {nâ‚ nâ‚‚ nâ‚â‚‚ : â„¤} (zâ‚ : Cochain F G nâ‚)
    (h : nâ‚ + nâ‚‚ = nâ‚â‚‚) : zâ‚.comp (0 : Cochain G K nâ‚‚) h = 0 := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by linarith), zero_v, comp_zero]

@[simp]
protected lemma comp_add {nâ‚ nâ‚‚ nâ‚â‚‚ : â„¤} (zâ‚ : Cochain F G nâ‚) (zâ‚‚ zâ‚‚' : Cochain G K nâ‚‚)
    (h : nâ‚ + nâ‚‚ = nâ‚â‚‚) : zâ‚.comp (zâ‚‚ + zâ‚‚') h = zâ‚.comp zâ‚‚ h + zâ‚.comp zâ‚‚' h := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by linarith), add_v, comp_add]

@[simp]
protected lemma comp_sub {nâ‚ nâ‚‚ nâ‚â‚‚ : â„¤} (zâ‚ : Cochain F G nâ‚) (zâ‚‚ zâ‚‚' : Cochain G K nâ‚‚)
    (h : nâ‚ + nâ‚‚ = nâ‚â‚‚) : zâ‚.comp (zâ‚‚ - zâ‚‚') h = zâ‚.comp zâ‚‚ h - zâ‚.comp zâ‚‚' h := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by linarith), sub_v, comp_sub]

@[simp]
protected lemma comp_neg {nâ‚ nâ‚‚ nâ‚â‚‚ : â„¤} (zâ‚ : Cochain F G nâ‚) (zâ‚‚ : Cochain G K nâ‚‚)
    (h : nâ‚ + nâ‚‚ = nâ‚â‚‚) : zâ‚.comp (-zâ‚‚) h = -zâ‚.comp zâ‚‚ h := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by linarith), neg_v, comp_neg]

@[simp]
protected lemma comp_smul {nâ‚ nâ‚‚ nâ‚â‚‚ : â„¤} (zâ‚ : Cochain F G nâ‚) (k : R) (zâ‚‚ : Cochain G K nâ‚‚)
    (h : nâ‚ + nâ‚‚ = nâ‚â‚‚ ) : zâ‚.comp (k â€¢ zâ‚‚) h = k â€¢ (zâ‚.comp zâ‚‚ h) := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by linarith), smul_v, Linear.comp_smul]

@[simp]
lemma comp_units_smul {nâ‚ nâ‚‚ nâ‚â‚‚ : â„¤} (zâ‚ : Cochain F G nâ‚) (k : RË£) (zâ‚‚ : Cochain G K nâ‚‚)
    (h : nâ‚ + nâ‚‚ = nâ‚â‚‚ ) : zâ‚.comp (k â€¢ zâ‚‚) h = k â€¢ (zâ‚.comp zâ‚‚ h) := by
  apply Cochain.comp_smul

@[simp]
protected lemma comp_id {n : â„¤} (zâ‚ : Cochain F G n) :
    zâ‚.comp (Cochain.ofHom (ðŸ™ G)) (add_zero n) = zâ‚ := by
  ext p q hpq
  simp only [comp_zero_cochain_v, ofHom_v, HomologicalComplex.id_f, comp_id]

@[simp]
lemma ofHoms_comp (Ï† : âˆ€ (p : â„¤), F.X p âŸ¶ G.X p) (Ïˆ : âˆ€ (p : â„¤), G.X p âŸ¶ K.X p) :
    (ofHoms Ï†).comp (ofHoms Ïˆ) (zero_add 0) = ofHoms (fun p => Ï† p â‰« Ïˆ p) := by aesop_cat

@[simp]
lemma ofHom_comp (f : F âŸ¶ G) (g : G âŸ¶ K) :
    ofHom (f â‰« g) = (ofHom f).comp (ofHom g) (zero_add 0) := by
  simp only [ofHom, HomologicalComplex.comp_f, ofHoms_comp]

variable (K)

/-- The differential on a cochain complex, as a cochain of degree `1`. -/
def diff : Cochain K K 1 := Cochain.mk (fun p q _ => K.d p q)

@[simp]
lemma diff_v (p q : â„¤) (hpq : p + 1 = q) : (diff K).v p q hpq = K.d p q := rfl

end Cochain

variable {F G}

/-- The differential on the complex of morphisms between cochain complexes. -/
def Î´ (z : Cochain F G n) : Cochain F G m :=
  Cochain.mk (fun p q hpq => z.v p (p + n) rfl â‰« G.d (p + n) q +
    m.negOnePow â€¢ F.d p (p + m - n) â‰« z.v (p + m - n) q (by rw [hpq, sub_add_cancel]))

/-! Similarly as for the composition of cochains, if `z : Cochain F G n`,
we usually need to carefully select intermediate indices with
good definitional properties in order to obtain a suitable expansion of the
morphisms which constitute `Î´ n m z : Cochain F G m` (when `n + 1 = m`, otherwise
it shall be zero). The basic equational lemma is `Î´_v` below. -/

lemma Î´_v (hnm : n + 1 = m) (z : Cochain F G n) (p q : â„¤) (hpq : p + m = q) (qâ‚ qâ‚‚ : â„¤)
    (hqâ‚ : qâ‚ = q - 1) (hqâ‚‚ : p + 1 = qâ‚‚) : (Î´ n m z).v p q hpq =
    z.v p qâ‚ (by rw [hqâ‚, â† hpq, â† hnm, â† add_assoc, add_sub_cancel]) â‰« G.d qâ‚ q
      + m.negOnePow â€¢ F.d p qâ‚‚ â‰« z.v qâ‚‚ q
          (by rw [â† hqâ‚‚, add_assoc, add_comm 1, hnm, hpq]) := by
  obtain rfl : qâ‚ = p + n := by linarith
  obtain rfl : qâ‚‚ = p + m - n := by linarith
  rfl

notation a " â€¢[" h "] " b:80 => Cochain.comp a b h

lemma Î´_eq (hnm : n + 1 = m) (z : Cochain F G n) :
    Î´ n m z = z â€¢[hnm] (Cochain.diff G) +
      m.negOnePow â€¢ (Cochain.diff F)â€¢[by rw [â† hnm, add_comm 1]] z := by
  ext p q hpq
  dsimp
  simp only [Î´_v n m hnm z p q hpq (q-1) (p+1) rfl rfl,
    Cochain.comp_v _ _ hnm p (q-1) q (by linarith) (by linarith),
    Cochain.comp_v _ _ (show 1+n = m by linarith) p (p+1) q (by linarith) (by linarith),
    Cochain.diff_v]

@[simp]
lemma Î´_zero_cochain_v (z : Cochain F G 0) (p q : â„¤) (hpq : p + 1 = q) :
    (Î´ 0 1 z).v p q hpq = z.v p p (add_zero p) â‰« G.d p q - F.d p q â‰« z.v q q (add_zero q):= by
  simp only [Î´_v 0 1 (zero_add 1) z p q hpq p q (by linarith) hpq,
    Int.negOnePow_one, Units.neg_smul, one_smul, sub_eq_add_neg]

lemma Î´_shape (hnm : Â¬ n + 1 = m) (z : Cochain F G n) : Î´ n m z = 0 := by
  ext p q hpq
  dsimp [Î´, Cochain.v, Cochain.mk]
  rw [F.shape, G.shape, comp_zero, zero_add, zero_comp, smul_zero]
  all_goals
    change Â¬ _=_
    rintro h
    apply hnm
    linarith

section

variable {n} {D : Type _} [Category D] [Preadditive D] (z z' : Cochain K L n) (f : K âŸ¶ L)
  (Î¦ : C â¥¤ D) [Î¦.Additive]

namespace Cochain

/-- If `Î¦ : C â¥¤ D` is an additive functor, a cochain `z : Cochain K L n` between
cochain complexes in `C` can be mapped to a cochain between the cochain complexes
in `D` obtained by applying the functor
`Î¦.mapHomologicalComplex _ : CochainComplex C â„¤ â¥¤ CochainComplex D â„¤`. -/
def map : Cochain ((Î¦.mapHomologicalComplex _).obj K) ((Î¦.mapHomologicalComplex _).obj L) n :=
  Cochain.mk (fun p q hpq => Î¦.map (z.v p q hpq))

@[simp]
lemma map_v (p q : â„¤) (hpq : p + n = q) : (z.map Î¦).v p q hpq = Î¦.map (z.v p q hpq) := rfl

@[simp]
lemma map_add : (z + z').map Î¦ = z.map Î¦ + z'.map Î¦ := by aesop_cat

@[simp]
lemma map_neg : (-z).map Î¦ = -z.map Î¦ := by aesop_cat

@[simp]
lemma map_sub : (z - z').map Î¦ = z.map Î¦ - z'.map Î¦ := by aesop_cat

variable (K L n)

@[simp]
lemma map_zero : (0 : Cochain K L n).map Î¦ = 0 := by aesop_cat

@[simp]
lemma map_comp {nâ‚ nâ‚‚ nâ‚â‚‚ : â„¤} (zâ‚ : Cochain F G nâ‚) (zâ‚‚ : Cochain G K nâ‚‚) (h : nâ‚ + nâ‚‚ = nâ‚â‚‚)
    (Î¦ : C â¥¤ D) [Î¦.Additive] :
    (Cochain.comp zâ‚ zâ‚‚ h).map Î¦ = Cochain.comp (zâ‚.map Î¦) (zâ‚‚.map Î¦) h := by
  ext p q hpq
  dsimp
  simp only [map_v, comp_v _ _ h p _ q rfl (by linarith), Î¦.map_comp]

@[simp]
lemma map_ofHom :
    (Cochain.ofHom f).map Î¦ = Cochain.ofHom ((Î¦.mapHomologicalComplex _).map f) := by aesop_cat

end Cochain

@[simp]
lemma Î´_map : Î´ n m (z.map Î¦) = (Î´ n m z).map Î¦ := by
  by_cases hnm : n + 1 = m
  Â· ext p q hpq
    dsimp
    simp only [Î´_v n m hnm _ p q hpq (q - 1) (p + 1) rfl rfl,
      Cochain.map_v, sub_add_cancel, Functor.mapHomologicalComplex_obj_d,
      Functor.map_add, Functor.map_comp, Functor.map_units_smul]
  Â· simp only [Î´_shape _ _ hnm, Cochain.map_zero]

variable (F G R n)

/-- The differential on the complex of morphisms between cochain complexes, as a linear map. -/
@[simps!]
def Î´_hom : Cochain F G n â†’â‚—[R] Cochain F G m where
  toFun := Î´ n m
  map_add' Î± Î² := by
    by_cases h : n + 1 = m
    Â· ext p q hpq
      dsimp
      simp only [Î´_v n m h _ p q hpq _ _ rfl rfl, Cochain.add_v, add_comp, comp_add, smul_add]
      abel
    Â· simp only [Î´_shape _ _ h, add_zero]
  map_smul' r a := by
    by_cases h : n + 1 = m
    Â· ext p q hpq
      dsimp
      simp only [Î´_v n m h _ p q hpq _ _ rfl rfl, Cochain.smul_v, Linear.comp_smul,
        Linear.smul_comp, smul_add, add_right_inj, smul_comm m.negOnePow r]
    Â· simp only [Î´_shape _ _ h, smul_zero]

variable {F G R}

@[simp] lemma Î´_add (zâ‚ zâ‚‚ : Cochain F G n) : Î´ n m (zâ‚ + zâ‚‚) = Î´ n m zâ‚ + Î´ n m zâ‚‚ :=
  (Î´_hom â„¤ F G n m).map_add zâ‚ zâ‚‚

@[simp] lemma Î´_sub (zâ‚ zâ‚‚ : Cochain F G n) : Î´ n m (zâ‚ - zâ‚‚) = Î´ n m zâ‚ - Î´ n m zâ‚‚ :=
  (Î´_hom â„¤ F G n m).map_sub zâ‚ zâ‚‚

@[simp] lemma Î´_zero : Î´ n m (0 : Cochain F G n) = 0 := (Î´_hom â„¤ F G n m).map_zero

@[simp] lemma Î´_neg (z : Cochain F G n) : Î´ n m (-z) = - Î´ n m z :=
  (Î´_hom â„¤ F G n m).map_neg z

@[simp] lemma Î´_smul (k : R) (z : Cochain F G n) : Î´ n m (k â€¢ z) = k â€¢ Î´ n m z :=
  (Î´_hom R F G n m).map_smul k z

@[simp] lemma Î´_units_smul (k : RË£) (z : Cochain F G n) : Î´ n m (k â€¢ z) = k â€¢ Î´ n m z :=
  Î´_smul ..

lemma Î´_Î´ (nâ‚€ nâ‚ nâ‚‚ : â„¤) (z : Cochain F G nâ‚€) : Î´ nâ‚ nâ‚‚ (Î´ nâ‚€ nâ‚ z) = 0 := by
  by_cases hâ‚â‚‚ : nâ‚ + 1 = nâ‚‚; swap; rw [Î´_shape _ _ hâ‚â‚‚]
  by_cases hâ‚€â‚ : nâ‚€ + 1 = nâ‚; swap; rw [Î´_shape _ _ hâ‚€â‚, Î´_zero]
  ext p q hpq
  dsimp
  simp only [Î´_v nâ‚ nâ‚‚ hâ‚â‚‚ _ p q hpq _ _ rfl rfl,
    Î´_v nâ‚€ nâ‚ hâ‚€â‚ z p (q-1) (by linarith) (q-2) _ (by linarith) rfl,
    Î´_v nâ‚€ nâ‚ hâ‚€â‚ z (p+1) q (by linarith) _ (p+2) rfl (by linarith),
    â† hâ‚â‚‚, Int.negOnePow_succ, add_comp, assoc,
    HomologicalComplex.d_comp_d, comp_zero, zero_add, comp_add,
    HomologicalComplex.d_comp_d_assoc, zero_comp, smul_zero,
    add_zero, add_right_neg, Units.neg_smul,
    Linear.units_smul_comp, Linear.comp_units_smul]

lemma Î´_comp {nâ‚ nâ‚‚ nâ‚â‚‚ : â„¤} (zâ‚ : Cochain F G nâ‚) (zâ‚‚ : Cochain G K nâ‚‚) (h : nâ‚ + nâ‚‚ = nâ‚â‚‚)
    (mâ‚ mâ‚‚ mâ‚â‚‚ : â„¤) (hâ‚â‚‚ : nâ‚â‚‚ + 1 = mâ‚â‚‚) (hâ‚ : nâ‚ + 1 = mâ‚) (hâ‚‚ : nâ‚‚ + 1 = mâ‚‚) :
    Î´ nâ‚â‚‚ mâ‚â‚‚ (zâ‚.comp zâ‚‚ h) = zâ‚.comp (Î´ nâ‚‚ mâ‚‚ zâ‚‚) (by rw [â† hâ‚â‚‚, â† hâ‚‚, â† h, add_assoc]) +
      nâ‚‚.negOnePow â€¢ (Î´ nâ‚ mâ‚ zâ‚).comp zâ‚‚
        (by rw [â† hâ‚â‚‚, â† hâ‚, â† h, add_assoc, add_comm 1, add_assoc]) := by
  subst hâ‚â‚‚ hâ‚ hâ‚‚ h
  ext p q hpq
  dsimp
  rw [zâ‚.comp_v _ (add_assoc nâ‚ nâ‚‚ 1).symm p _ q rfl (by linarith),
    Cochain.comp_v _ _ (show nâ‚ + 1 + nâ‚‚ = nâ‚ + nâ‚‚ + 1 by linarith) p (p+nâ‚+1) q
      (by linarith) (by linarith),
    Î´_v (nâ‚ + nâ‚‚) _ rfl (zâ‚.comp zâ‚‚ rfl) p q hpq (p + nâ‚ + nâ‚‚) _ (by linarith) rfl,
    zâ‚.comp_v zâ‚‚ rfl p _ _ rfl rfl,
    zâ‚.comp_v zâ‚‚ rfl (p+1) (p+nâ‚+1) q (by linarith) (by linarith),
    Î´_v nâ‚‚ (nâ‚‚+1) rfl zâ‚‚ (p+nâ‚) q (by linarith) (p+nâ‚+nâ‚‚) _ (by linarith) rfl,
    Î´_v nâ‚ (nâ‚+1) rfl zâ‚ p (p+nâ‚+1) (by linarith) (p+nâ‚) _ (by linarith) rfl]
  simp only [assoc, comp_add, add_comp, Int.negOnePow_succ, Int.negOnePow_add nâ‚ nâ‚‚,
    Units.neg_smul, comp_neg, neg_comp, smul_neg, smul_smul, Linear.units_smul_comp,
    mul_comm nâ‚.negOnePow nâ‚‚.negOnePow, Linear.comp_units_smul, smul_add]
  abel

lemma Î´_zero_cochain_comp {nâ‚‚ : â„¤} (zâ‚ : Cochain F G 0) (zâ‚‚ : Cochain G K nâ‚‚)
    (mâ‚‚ : â„¤) (hâ‚‚ : nâ‚‚ + 1 = mâ‚‚) :
    Î´ nâ‚‚ mâ‚‚ (zâ‚.comp zâ‚‚ (zero_add nâ‚‚)) =
      zâ‚.comp (Î´ nâ‚‚ mâ‚‚ zâ‚‚) (zero_add mâ‚‚) +
      nâ‚‚.negOnePow â€¢ ((Î´ 0 1 zâ‚).comp zâ‚‚ (by rw [add_comm, hâ‚‚])) :=
  Î´_comp zâ‚ zâ‚‚ (zero_add nâ‚‚) 1 mâ‚‚ mâ‚‚ hâ‚‚ (zero_add 1) hâ‚‚

lemma Î´_comp_zero_cochain {nâ‚ : â„¤} (zâ‚ : Cochain F G nâ‚) (zâ‚‚ : Cochain G K 0)
    (mâ‚ : â„¤) (hâ‚ : nâ‚ + 1 = mâ‚) :
    Î´ nâ‚ mâ‚ (zâ‚.comp zâ‚‚ (add_zero nâ‚)) =
      zâ‚.comp (Î´ 0 1 zâ‚‚) hâ‚ + (Î´ nâ‚ mâ‚ zâ‚).comp zâ‚‚ (add_zero mâ‚) := by
  simp only [Î´_comp zâ‚ zâ‚‚ (add_zero nâ‚) mâ‚ 1 mâ‚ hâ‚ hâ‚ (zero_add 1), one_smul,
    Int.negOnePow_zero]

@[simp]
lemma Î´_ofHom {p : â„¤} (Ï† : F âŸ¶ G) : Î´ 0 p (Cochain.ofHom Ï†) = 0 := by
  by_cases h : p = 1
  Â· subst h
    ext
    simp
  Â· rw [Î´_shape]
    intro
    exact h (by linarith)

@[simp]
lemma Î´_ofHomotopy {Ï†â‚ Ï†â‚‚ : F âŸ¶ G} (h : Homotopy Ï†â‚ Ï†â‚‚) :
    Î´ (-1) 0 (Cochain.ofHomotopy h) = Cochain.ofHom Ï†â‚ - Cochain.ofHom Ï†â‚‚ := by
  ext p
  have eq := h.comm p
  rw [dNext_eq h.hom (show (ComplexShape.up â„¤).Rel p (p+1) by simp),
    prevD_eq h.hom (show (ComplexShape.up â„¤).Rel (p-1) p by simp)] at eq
  rw [Cochain.ofHomotopy, Î´_v (-1) 0 (neg_add_self 1) _ p p (add_zero p) (p-1) (p+1) rfl rfl]
  simp only [Cochain.mk_v, add_left_neg, one_smul, Int.negOnePow_zero,
    Cochain.sub_v, Cochain.ofHom_v, eq]
  abel

lemma Î´_neg_one_cochain (z : Cochain F G (-1)) :
    Î´ (-1) 0 z = Cochain.ofHom (Homotopy.nullHomotopicMap'
      (fun i j hij => z.v i j (by dsimp at hij; rw [â† hij, add_neg_cancel_right]))) := by
  ext p
  rw [Î´_v (-1) 0 (neg_add_self 1) _ p p (add_zero p) (p-1) (p+1) rfl rfl]
  simp only [neg_add_self, one_smul, Cochain.ofHom_v, Int.negOnePow_zero]
  rw [Homotopy.nullHomotopicMap'_f (show (ComplexShape.up â„¤).Rel (p-1) p by simp)
    (show (ComplexShape.up â„¤).Rel p (p+1) by simp)]
  abel

end

end HomComplex

variable (F G)

open HomComplex

/-- The cochain complex of homomorphisms between two cochain complexes `F` and `G`.
In degree `n : â„¤`, it consists of the abelian group `HomComplex.Cochain F G n`. -/
@[simps! X d_apply]
def HomComplex : CochainComplex AddCommGroupCat â„¤ where
  X i := AddCommGroupCat.of (Cochain F G i)
  d i j := AddCommGroupCat.ofHom (Î´_hom â„¤ F G i j)
  shape _ _ hij := by ext; apply Î´_shape _ _ hij
  d_comp_d' _ _ _ _ _  := by ext; apply Î´_Î´

namespace HomComplex

/-- The subgroup of cocycles in `Cochain F G n`. -/
def cocycle : AddSubgroup (Cochain F G n) :=
  AddMonoidHom.ker (Î´_hom â„¤ F G n (n + 1)).toAddMonoidHom

/-- The type of `n`-cocycles, as a subtype of `Cochain F G n`. -/
def Cocycle : Type v := cocycle F G n

instance : AddCommGroup (Cocycle F G n) := by
  dsimp only [Cocycle]
  infer_instance

namespace Cocycle

variable {F G}

lemma mem_iff (hnm : n + 1 = m) (z : Cochain F G n) :
    z âˆˆ cocycle F G n â†” Î´ n m z = 0 := by subst hnm; rfl

variable {n}

instance : Coe (Cocycle F G n) (Cochain F G n) where
  coe x := x.1

@[ext]
lemma ext (zâ‚ zâ‚‚ : Cocycle F G n) (h : (zâ‚ : Cochain F G n) = zâ‚‚) : zâ‚ = zâ‚‚ :=
  Subtype.ext h

lemma ext_iff (zâ‚ zâ‚‚ : Cocycle F G n) : zâ‚ = zâ‚‚ â†” (zâ‚ : Cochain F G n) = zâ‚‚ :=
  Subtype.ext_iff

instance : SMul R (Cocycle F G n) where
  smul r z := âŸ¨r â€¢ z.1, by
    have hz := z.2
    rw [mem_iff n (n + 1) rfl] at hz âŠ¢
    simp only [Î´_smul, hz, smul_zero]âŸ©

variable (F G n)

@[simp]
lemma coe_zero : (â†‘(0 : Cocycle F G n) : Cochain F G n) = 0 := by rfl

variable {F G n}

@[simp]
lemma coe_add (zâ‚ zâ‚‚ : Cocycle F G n) :
    (â†‘(zâ‚ + zâ‚‚) : Cochain F G n) = (zâ‚ : Cochain F G n) + (zâ‚‚ : Cochain F G n) := rfl

@[simp]
lemma coe_neg (z : Cocycle F G n) :
    (â†‘(-z) : Cochain F G n) = -(z : Cochain F G n) := rfl

@[simp]
lemma coe_smul (z : Cocycle F G n) (x : R) :
    (â†‘(x â€¢ z) : Cochain F G n) = x â€¢ (z : Cochain F G n) := rfl

@[simp]
lemma coe_units_smul (z : Cocycle F G n) (x : RË£) :
    (â†‘(x â€¢ z) : Cochain F G n) = x â€¢ (z : Cochain F G n) := rfl

@[simp]
lemma coe_sub (zâ‚ zâ‚‚ : Cocycle F G n) :
    (â†‘(zâ‚ - zâ‚‚) : Cochain F G n) = (zâ‚ : Cochain F G n) - (zâ‚‚ : Cochain F G n) := rfl

instance : Module R (Cocycle F G n) where
  one_smul _ := by aesop
  mul_smul _ _ _ := by ext; dsimp; rw [smul_smul]
  smul_zero _ := by aesop
  smul_add _ _ _ := by aesop
  add_smul _ _ _ := by ext; dsimp; rw [add_smul]
  zero_smul := by aesop

/-- Constructor for `Cocycle F G n`, taking as inputs `z : Cochain F G n`, an integer
`m : â„¤` such that `n + 1 = m`, and the relation `Î´ n m z = 0`. -/
@[simps]
def mk (z : Cochain F G n) (m : â„¤) (hnm : n + 1 = m) (h : Î´ n m z = 0) : Cocycle F G n :=
  âŸ¨z, by simpa only [mem_iff n m hnm z] using hâŸ©

@[simp]
lemma Î´_eq_zero {n : â„¤} (z : Cocycle F G n) (m : â„¤) : Î´ n m (z : Cochain F G n) = 0 := by
  by_cases h : n + 1 = m
  Â· rw [â† mem_iff n m h]
    exact z.2
  Â· exact Î´_shape n m h _

/-- The `0`-cocycle associated to a morphism in `CochainComplex C â„¤`. -/
@[simps!]
def ofHom (Ï† : F âŸ¶ G) : Cocycle F G 0 := mk (Cochain.ofHom Ï†) 1 (zero_add 1) (by simp)

/-- The morphism in `CochainComplex C â„¤` associated to a `0`-cocycle. -/
@[simps]
def homOf (z : Cocycle F G 0) : F âŸ¶ G where
  f i := (z : Cochain _ _ _).v i i (add_zero i)
  comm' := by
    rintro i j rfl
    rcases z with âŸ¨z, hzâŸ©
    dsimp
    rw [mem_iff 0 1 (zero_add 1)] at hz
    simpa only [Î´_zero_cochain_v, Cochain.zero_v, sub_eq_zero]
      using Cochain.congr_v hz i (i + 1) rfl

@[simp]
lemma homOf_ofHom_eq_self (Ï† : F âŸ¶ G) : homOf (ofHom Ï†) = Ï† := by aesop_cat

@[simp]
lemma ofHom_homOf_eq_self (z : Cocycle F G 0) : ofHom (homOf z) = z := by aesop_cat

@[simp]
lemma cochain_ofHom_homOf_eq_coe (z : Cocycle F G 0) :
    Cochain.ofHom (homOf z) = (z : Cochain F G 0) := by
  simpa only [ext_iff] using ofHom_homOf_eq_self z

variable (F G)

/-- The additive equivalence between morphisms in `CochainComplex C â„¤` and `0`-cocycles. -/
@[simps]
def equivHom : (F âŸ¶ G) â‰ƒ+ Cocycle F G 0 where
  toFun := ofHom
  invFun := homOf
  left_inv := homOf_ofHom_eq_self
  right_inv := ofHom_homOf_eq_self
  map_add' := by aesop_cat

variable (K)

/-- The `1`-cocycle given by the differential on a cochain complex. -/
@[simps!]
def diff : Cocycle K K 1 :=
  Cocycle.mk (Cochain.diff K) 2 rfl (by
    ext p q hpq
    simp only [Cochain.zero_v, Î´_v 1 2 rfl _ p q hpq _ _ rfl rfl, Cochain.diff_v,
      HomologicalComplex.d_comp_d, smul_zero, add_zero])

section

variable {K}
variable {D : Type _} [Category D] [Preadditive D] (z z' : Cocycle K L n) (f : K âŸ¶ L)
  (Î¦ : C â¥¤ D) [Î¦.Additive]

@[simps!]
def map : Cocycle ((Î¦.mapHomologicalComplex _).obj K) ((Î¦.mapHomologicalComplex _).obj L) n :=
  Cocycle.mk ((z : Cochain K L n).map Î¦) (n+1) rfl (by simp)

@[simp]
lemma map_add : Cocycle.map (z + z') Î¦ = Cocycle.map z Î¦ + Cocycle.map z' Î¦ := by aesop_cat

@[simp]
lemma map_neg : Cocycle.map (-z) Î¦ = -Cocycle.map z Î¦ := by aesop_cat

@[simp]
lemma map_sub : Cocycle.map (z-z') Î¦ = Cocycle.map z Î¦ - Cocycle.map z' Î¦ := by aesop_cat

@[simp]
lemma map_of_hom : Cocycle.map (Cocycle.ofHom f) Î¦ =
  Cocycle.ofHom ((Î¦.mapHomologicalComplex _).map f) := by aesop_cat

variable (K L n)

@[simp]
lemma map_zero : Cocycle.map (0 : Cocycle K L n) Î¦ = 0 := by aesop_cat

end

end Cocycle
variable {F G}

@[simp]
lemma Î´_comp_zero_cocycle {n : â„¤} (zâ‚ : Cochain F G n) (zâ‚‚ : Cocycle G K 0) (m : â„¤) :
    Î´ n m (zâ‚.comp zâ‚‚.1 (add_zero n)) =
      (Î´ n m zâ‚).comp zâ‚‚.1 (add_zero m) := by
  by_cases hnm : n + 1 = m
  Â· simp [Î´_comp_zero_cochain _ _ _ hnm]
  Â· simp [Î´_shape _ _ hnm]

@[simp]
lemma Î´_comp_ofHom {n : â„¤} (zâ‚ : Cochain F G n) (f : G âŸ¶ K) (m : â„¤) :
    Î´ n m (zâ‚.comp (Cochain.ofHom f) (add_zero n)) =
      (Î´ n m zâ‚).comp (Cochain.ofHom f) (add_zero m) := by
  rw [â† Cocycle.ofHom_coe, Î´_comp_zero_cocycle]


@[simp]
lemma Î´_zero_cocycle_comp {n : â„¤} (zâ‚ : Cocycle F G 0) (zâ‚‚ : Cochain G K n) (m : â„¤) :
    Î´ n m (zâ‚.1.comp zâ‚‚ (zero_add n)) =
      zâ‚.1.comp (Î´ n m zâ‚‚) (zero_add m) := by
  by_cases hnm : n + 1 = m
  Â· simp [Î´_zero_cochain_comp _ _ _ hnm]
  Â· simp [Î´_shape _ _ hnm]

@[simp]
lemma Î´_ofHom_comp {n : â„¤} (f : F âŸ¶ G) (z : Cochain G K n) (m : â„¤) :
    Î´ n m ((Cochain.ofHom f).comp z (zero_add n)) =
      (Cochain.ofHom f).comp (Î´ n m z) (zero_add m) := by
  rw [â† Cocycle.ofHom_coe, Î´_zero_cocycle_comp]

namespace Cochain

/-- Given two morphisms of complexes `Ï†â‚ Ï†â‚‚ : F âŸ¶ G`, the datum of an homotopy between `Ï†â‚` and
`Ï†â‚‚` is equivalent to the datum of a `1`-cochain `z` such that `Î´ (-1) 0 z` is the difference
of the zero cochains associated to `Ï†â‚‚` and `Ï†â‚`. -/
@[simps]
def equivHomotopy (Ï†â‚ Ï†â‚‚ : F âŸ¶ G) :
    Homotopy Ï†â‚ Ï†â‚‚ â‰ƒ
      { z : Cochain F G (-1) // Cochain.ofHom Ï†â‚ = Î´ (-1) 0 z + Cochain.ofHom Ï†â‚‚ } where
  toFun ho := âŸ¨Cochain.ofHomotopy ho, by simp only [Î´_ofHomotopy, sub_add_cancel]âŸ©
  invFun z :=
    { hom := fun i j => if hij : i + (-1) = j then z.1.v i j hij else 0
      zero := fun i j (hij : j + 1 â‰  i) => dif_neg (fun _ => hij (by linarith))
      comm := fun p => by
        have eq := Cochain.congr_v z.2 p p (add_zero p)
        have hâ‚ : (ComplexShape.up â„¤).Rel (p - 1) p := by simp
        have hâ‚‚ : (ComplexShape.up â„¤).Rel p (p + 1) := by simp
        simp only [Î´_neg_one_cochain, Cochain.ofHom_v, ComplexShape.up_Rel, Cochain.add_v,
          Homotopy.nullHomotopicMap'_f hâ‚ hâ‚‚] at eq
        rw [dNext_eq _ hâ‚‚, prevD_eq _ hâ‚, eq, dif_pos, dif_pos] }
  left_inv := fun ho => by
    ext i j
    dsimp
    split_ifs with h
    Â· rfl
    Â· rw [ho.zero i j (fun h' => h (by dsimp at h'; linarith))]
  right_inv := fun z => by
    ext p q hpq
    dsimp [Cochain.ofHomotopy]
    rw [dif_pos hpq]

lemma equivHomotopy_apply_of_eq {Ï†â‚ Ï†â‚‚ : F âŸ¶ G} (h : Ï†â‚ = Ï†â‚‚) :
    (equivHomotopy _ _ (Homotopy.ofEq h)).1 = 0 := rfl

lemma ofHom_injective {fâ‚ fâ‚‚ : F âŸ¶ G} (h : ofHom fâ‚ = ofHom fâ‚‚) : fâ‚ = fâ‚‚ :=
  (Cocycle.equivHom F G).injective (by ext1; exact h)

def single {p q : â„¤} (f : K.X p âŸ¶ L.X q) (n : â„¤) :
    Cochain K L n :=
  Cochain.mk (fun p' q' _ =>
    if h : p = p' âˆ§ q = q'
      then (K.XIsoOfEq h.1).inv â‰« f â‰« (L.XIsoOfEq h.2).hom
      else 0)

@[simp]
lemma single_v {p q : â„¤} (f : K.X p âŸ¶ L.X q) (n : â„¤) (hpq : p + n = q) :
    (single f n).v p q hpq = f := by
  dsimp [single]
  rw [if_pos, id_comp, comp_id]
  tauto

lemma single_v_eq_zero {p q : â„¤} (f : K.X p âŸ¶ L.X q) (n : â„¤) (p' q' : â„¤) (hpq' : p' + n = q')
    (hp' : p' â‰  p) :
    (single f n).v p' q' hpq' = 0 := by
  dsimp [single]
  rw [dif_neg]
  intro h
  exact hp' (by linarith)

lemma single_v_eq_zero' {p q : â„¤} (f : K.X p âŸ¶ L.X q) (n : â„¤) (p' q' : â„¤) (hpq' : p' + n = q')
    (hq' : q' â‰  q) :
    (single f n).v p' q' hpq' = 0 := by
  dsimp [single]
  rw [dif_neg]
  intro h
  exact hq' (by linarith)

lemma Î´_single {p q : â„¤} (f : K.X p âŸ¶ L.X q) (n m : â„¤) (hm : n + 1 = m)
    (p' q' : â„¤) (hp' : p' + 1 = p) (hq' : q + 1 = q') :
    Î´ n m (single f n) = single (f â‰« L.d q q') m + m.negOnePow â€¢ single (K.d p' p â‰« f) m := by
  ext p'' q'' hpq''
  rw [Î´_v n m hm (single f n) p'' q'' (by linarith) (q''-1) (p''+1) rfl (by linarith),
    add_v, units_smul_v]
  congr 1
  Â· by_cases h : p'' = p
    Â· subst h
      by_cases h : q = q'' - 1
      Â· subst h
        obtain rfl : q' = q'' := by linarith
        simp only [single_v]
      Â· rw [single_v_eq_zero', single_v_eq_zero', zero_comp]
        all_goals
          intro
          exact h (by linarith)
    Â· rw [single_v_eq_zero _ _ _ _ _ h, single_v_eq_zero _ _ _ _ _ h, zero_comp]
  Â· subst hm
    by_cases h : q'' = q
    Â· subst h
      by_cases h : p'' = p'
      Â· subst h
        obtain rfl : p = p''+1 := by linarith
        simp
      Â· rw [single_v_eq_zero _ _ _ _ _ h, single_v_eq_zero, comp_zero, smul_zero]
        intro
        apply h
        linarith
    Â· simp only [single_v_eq_zero' _ _ _ _ _ h, comp_zero, smul_zero]

end Cochain

end HomComplex

end CochainComplex
