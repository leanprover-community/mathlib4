/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Category.Grp.Preadditive
import Mathlib.Algebra.Homology.Homotopy
import Mathlib.Algebra.Module.Pi
import Mathlib.Algebra.Ring.NegOnePow
import Mathlib.CategoryTheory.Linear.LinearFunctor
import Mathlib.Tactic.Linarith

/-! The cochain complex of homomorphisms between cochain complexes

If `F` and `G` are cochain complexes (indexed by `‚Ñ§`) in a preadditive category,
there is a cochain complex of abelian groups whose `0`-cocycles identify to
morphisms `F ‚ü∂ G`. Informally, in degree `n`, this complex shall consist of
cochains of degree `n` from `F` to `G`, i.e. arbitrary families for morphisms
`F.X p ‚ü∂ G.X (p + n)`. This complex shall be denoted `HomComplex F G`.

In order to avoid type theoretic issues, a cochain of degree `n : ‚Ñ§`
(i.e. a term of type of `Cochain F G n`) shall be defined here
as the data of a morphism `F.X p ‚ü∂ G.X q` for all triplets
`‚ü®p, q, hpq‚ü©` where `p` and `q` are integers and `hpq : p + n = q`.
If `Œ± : Cochain F G n`, we shall define `Œ±.v p q hpq : F.X p ‚ü∂ G.X q`.

We follow the signs conventions appearing in the introduction of
[Brian Conrad's book *Grothendieck duality and base change*][conrad2000].

## References
* [Brian Conrad, Grothendieck duality and base change][conrad2000]

-/

assert_not_exists TwoSidedIdeal

open CategoryTheory Category Limits Preadditive

universe v u

variable {C : Type u} [Category.{v} C] [Preadditive C] {R : Type*} [Ring R] [Linear R C]

namespace CochainComplex

variable {F G K L : CochainComplex C ‚Ñ§} (n m : ‚Ñ§)

namespace HomComplex

/-- A term of type `HomComplex.Triplet n` consists of two integers `p` and `q`
such that `p + n = q`. (This type is introduced so that the instance
`AddCommGroup (Cochain F G n)` defined below can be found automatically.) -/
structure Triplet (n : ‚Ñ§) where
  /-- a first integer -/
  p : ‚Ñ§
  /-- a second integer -/
  q : ‚Ñ§
  /-- the condition on the two integers -/
  hpq : p + n = q

variable (F G)

/-- A cochain of degree `n : ‚Ñ§` between to cochain complexes `F` and `G` consists
of a family of morphisms `F.X p ‚ü∂ G.X q` whenever `p + n = q`, i.e. for all
triplets in `HomComplex.Triplet n`. -/
def Cochain := ‚àÄ (T : Triplet n), F.X T.p ‚ü∂ G.X T.q

instance : AddCommGroup (Cochain F G n) := by
  dsimp only [Cochain]
  infer_instance

instance : Module R (Cochain F G n) := by
  dsimp only [Cochain]
  infer_instance

namespace Cochain

variable {F G n}

/-- A practical constructor for cochains. -/
def mk (v : ‚àÄ (p q : ‚Ñ§) (_ : p + n = q), F.X p ‚ü∂ G.X q) : Cochain F G n :=
  fun ‚ü®p, q, hpq‚ü© => v p q hpq

/-- The value of a cochain on a triplet `‚ü®p, q, hpq‚ü©`. -/
def v (Œ≥ : Cochain F G n) (p q : ‚Ñ§) (hpq : p + n = q) :
    F.X p ‚ü∂ G.X q := Œ≥ ‚ü®p, q, hpq‚ü©

@[simp]
lemma mk_v (v : ‚àÄ (p q : ‚Ñ§) (_ : p + n = q), F.X p ‚ü∂ G.X q) (p q : ‚Ñ§) (hpq : p + n = q) :
    (Cochain.mk v).v p q hpq = v p q hpq := rfl

lemma congr_v {z‚ÇÅ z‚ÇÇ : Cochain F G n} (h : z‚ÇÅ = z‚ÇÇ) (p q : ‚Ñ§) (hpq : p + n = q) :
    z‚ÇÅ.v p q hpq = z‚ÇÇ.v p q hpq := by subst h; rfl

@[ext]
lemma ext (z‚ÇÅ z‚ÇÇ : Cochain F G n)
    (h : ‚àÄ (p q hpq), z‚ÇÅ.v p q hpq = z‚ÇÇ.v p q hpq) : z‚ÇÅ = z‚ÇÇ := by
  funext ‚ü®p, q, hpq‚ü©
  apply h

@[ext 1100]
lemma ext‚ÇÄ (z‚ÇÅ z‚ÇÇ : Cochain F G 0)
    (h : ‚àÄ (p : ‚Ñ§), z‚ÇÅ.v p p (add_zero p) = z‚ÇÇ.v p p (add_zero p)) : z‚ÇÅ = z‚ÇÇ := by
  ext
  grind

@[simp]
lemma zero_v {n : ‚Ñ§} (p q : ‚Ñ§) (hpq : p + n = q) :
    (0 : Cochain F G n).v p q hpq = 0 := rfl

@[simp]
lemma add_v {n : ‚Ñ§} (z‚ÇÅ z‚ÇÇ : Cochain F G n) (p q : ‚Ñ§) (hpq : p + n = q) :
    (z‚ÇÅ + z‚ÇÇ).v p q hpq = z‚ÇÅ.v p q hpq + z‚ÇÇ.v p q hpq := rfl

@[simp]
lemma sub_v {n : ‚Ñ§} (z‚ÇÅ z‚ÇÇ : Cochain F G n) (p q : ‚Ñ§) (hpq : p + n = q) :
    (z‚ÇÅ - z‚ÇÇ).v p q hpq = z‚ÇÅ.v p q hpq - z‚ÇÇ.v p q hpq := rfl

@[simp]
lemma neg_v {n : ‚Ñ§} (z : Cochain F G n) (p q : ‚Ñ§) (hpq : p + n = q) :
    (-z).v p q hpq = - (z.v p q hpq) := rfl

@[simp]
lemma smul_v {n : ‚Ñ§} (k : R) (z : Cochain F G n) (p q : ‚Ñ§) (hpq : p + n = q) :
    (k ‚Ä¢ z).v p q hpq = k ‚Ä¢ (z.v p q hpq) := rfl

@[simp]
lemma units_smul_v {n : ‚Ñ§} (k : RÀ£) (z : Cochain F G n) (p q : ‚Ñ§) (hpq : p + n = q) :
    (k ‚Ä¢ z).v p q hpq = k ‚Ä¢ (z.v p q hpq) := rfl

/-- A cochain of degree `0` from `F` to `G` can be constructed from a family
of morphisms `F.X p ‚ü∂ G.X p` for all `p : ‚Ñ§`. -/
def ofHoms (œà : ‚àÄ (p : ‚Ñ§), F.X p ‚ü∂ G.X p) : Cochain F G 0 :=
  Cochain.mk (fun p q hpq => œà p ‚â´ eqToHom (by rw [‚Üê hpq, add_zero]))

@[simp]
lemma ofHoms_v (œà : ‚àÄ (p : ‚Ñ§), F.X p ‚ü∂ G.X p) (p : ‚Ñ§) :
    (ofHoms œà).v p p (add_zero p) = œà p := by
  simp only [ofHoms, mk_v, eqToHom_refl, comp_id]

@[simp]
lemma ofHoms_zero : ofHoms (fun p => (0 : F.X p ‚ü∂ G.X p)) = 0 := by cat_disch

@[simp]
lemma ofHoms_v_comp_d (œà : ‚àÄ (p : ‚Ñ§), F.X p ‚ü∂ G.X p) (p q q' : ‚Ñ§) (hpq : p + 0 = q) :
    (ofHoms œà).v p q hpq ‚â´ G.d q q' = œà p ‚â´ G.d p q' := by
  rw [add_zero] at hpq
  subst hpq
  rw [ofHoms_v]

@[simp]
lemma d_comp_ofHoms_v (œà : ‚àÄ (p : ‚Ñ§), F.X p ‚ü∂ G.X p) (p' p q : ‚Ñ§) (hpq : p + 0 = q) :
    F.d p' p ‚â´ (ofHoms œà).v p q hpq = F.d p' q ‚â´ œà q := by
  rw [add_zero] at hpq
  subst hpq
  rw [ofHoms_v]

/-- The `0`-cochain attached to a morphism of cochain complexes. -/
def ofHom (œÜ : F ‚ü∂ G) : Cochain F G 0 := ofHoms (fun p => œÜ.f p)

variable (F G)

@[simp]
lemma ofHom_zero : ofHom (0 : F ‚ü∂ G) = 0 := by
  simp only [ofHom, HomologicalComplex.zero_f_apply, ofHoms_zero]

variable {F G}

@[simp]
lemma ofHom_v (œÜ : F ‚ü∂ G) (p : ‚Ñ§) : (ofHom œÜ).v p p (add_zero p) = œÜ.f p := by
  simp only [ofHom, ofHoms_v]

@[simp]
lemma ofHom_v_comp_d (œÜ : F ‚ü∂ G) (p q q' : ‚Ñ§) (hpq : p + 0 = q) :
    (ofHom œÜ).v p q hpq ‚â´ G.d q q' = œÜ.f p ‚â´ G.d p q' := by
  simp only [ofHom, ofHoms_v_comp_d]

@[simp]
lemma d_comp_ofHom_v (œÜ : F ‚ü∂ G) (p' p q : ‚Ñ§) (hpq : p + 0 = q) :
    F.d p' p ‚â´ (ofHom œÜ).v p q hpq = F.d p' q ‚â´ œÜ.f q := by
  simp only [ofHom, d_comp_ofHoms_v]

@[simp]
lemma ofHom_add (œÜ‚ÇÅ œÜ‚ÇÇ : F ‚ü∂ G) :
    Cochain.ofHom (œÜ‚ÇÅ + œÜ‚ÇÇ) = Cochain.ofHom œÜ‚ÇÅ + Cochain.ofHom œÜ‚ÇÇ := by cat_disch

@[simp]
lemma ofHom_sub (œÜ‚ÇÅ œÜ‚ÇÇ : F ‚ü∂ G) :
    Cochain.ofHom (œÜ‚ÇÅ - œÜ‚ÇÇ) = Cochain.ofHom œÜ‚ÇÅ - Cochain.ofHom œÜ‚ÇÇ := by cat_disch

@[simp]
lemma ofHom_neg (œÜ : F ‚ü∂ G) :
    Cochain.ofHom (-œÜ) = -Cochain.ofHom œÜ := by cat_disch

/-- The cochain of degree `-1` given by an homotopy between two morphism of complexes. -/
def ofHomotopy {œÜ‚ÇÅ œÜ‚ÇÇ : F ‚ü∂ G} (ho : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) : Cochain F G (-1) :=
  Cochain.mk (fun p q _ => ho.hom p q)

@[simp]
lemma ofHomotopy_ofEq {œÜ‚ÇÅ œÜ‚ÇÇ : F ‚ü∂ G} (h : œÜ‚ÇÅ = œÜ‚ÇÇ) :
    ofHomotopy (Homotopy.ofEq h) = 0 := rfl

@[simp]
lemma ofHomotopy_refl (œÜ : F ‚ü∂ G) :
    ofHomotopy (Homotopy.refl œÜ) = 0 := rfl

@[reassoc]
lemma v_comp_XIsoOfEq_hom
    (Œ≥ : Cochain F G n) (p q q' : ‚Ñ§) (hpq : p + n = q) (hq' : q = q') :
    Œ≥.v p q hpq ‚â´ (HomologicalComplex.XIsoOfEq G hq').hom = Œ≥.v p q' (by rw [‚Üê hq', hpq]) := by
  subst hq'
  simp only [HomologicalComplex.XIsoOfEq, eqToIso_refl, Iso.refl_hom, comp_id]

@[reassoc]
lemma v_comp_XIsoOfEq_inv
    (Œ≥ : Cochain F G n) (p q q' : ‚Ñ§) (hpq : p + n = q) (hq' : q' = q) :
    Œ≥.v p q hpq ‚â´ (HomologicalComplex.XIsoOfEq G hq').inv = Œ≥.v p q' (by rw [hq', hpq]) := by
  subst hq'
  simp only [HomologicalComplex.XIsoOfEq, eqToIso_refl, Iso.refl_inv, comp_id]

/-- The composition of cochains. -/
def comp {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ : Cochain F G n‚ÇÅ) (z‚ÇÇ : Cochain G K n‚ÇÇ) (h : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ) :
    Cochain F K n‚ÇÅ‚ÇÇ :=
  Cochain.mk (fun p q hpq => z‚ÇÅ.v p (p + n‚ÇÅ) rfl ‚â´ z‚ÇÇ.v (p + n‚ÇÅ) q (by omega))

/-! If `z‚ÇÅ` is a cochain of degree `n‚ÇÅ` and `z‚ÇÇ` is a cochain of degree `n‚ÇÇ`, and that
we have a relation `h : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ`, then `z‚ÇÅ.comp z‚ÇÇ h` is a cochain of degree `n‚ÇÅ‚ÇÇ`.
The following lemma `comp_v` computes the value of this composition `z‚ÇÅ.comp z‚ÇÇ h`
on a triplet `‚ü®p‚ÇÅ, p‚ÇÉ, _‚ü©` (with `p‚ÇÅ + n‚ÇÅ‚ÇÇ = p‚ÇÉ`). In order to use this lemma,
we need to provide an intermediate integer `p‚ÇÇ` such that `p‚ÇÅ + n‚ÇÅ = p‚ÇÇ`.
It is advisable to use a `p‚ÇÇ` that has good definitional properties
(i.e. `p‚ÇÅ + n‚ÇÅ` is not always the best choice.)

When `z‚ÇÅ` or `z‚ÇÇ` is a `0`-cochain, there is a better choice of `p‚ÇÇ`, and this leads
to the two simplification lemmas `comp_zero_cochain_v` and `zero_cochain_comp_v`.

-/

lemma comp_v {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ : Cochain F G n‚ÇÅ) (z‚ÇÇ : Cochain G K n‚ÇÇ) (h : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ)
    (p‚ÇÅ p‚ÇÇ p‚ÇÉ : ‚Ñ§) (h‚ÇÅ : p‚ÇÅ + n‚ÇÅ = p‚ÇÇ) (h‚ÇÇ : p‚ÇÇ + n‚ÇÇ = p‚ÇÉ) :
    (z‚ÇÅ.comp z‚ÇÇ h).v p‚ÇÅ p‚ÇÉ (by rw [‚Üê h‚ÇÇ, ‚Üê h‚ÇÅ, ‚Üê h, add_assoc]) =
      z‚ÇÅ.v p‚ÇÅ p‚ÇÇ h‚ÇÅ ‚â´ z‚ÇÇ.v p‚ÇÇ p‚ÇÉ h‚ÇÇ := by
  subst h‚ÇÅ; rfl

@[simp]
lemma comp_zero_cochain_v (z‚ÇÅ : Cochain F G n) (z‚ÇÇ : Cochain G K 0) (p q : ‚Ñ§) (hpq : p + n = q) :
    (z‚ÇÅ.comp z‚ÇÇ (add_zero n)).v p q hpq = z‚ÇÅ.v p q hpq ‚â´ z‚ÇÇ.v q q (add_zero q) :=
  comp_v z‚ÇÅ z‚ÇÇ (add_zero n) p q q hpq (add_zero q)

@[simp]
lemma zero_cochain_comp_v (z‚ÇÅ : Cochain F G 0) (z‚ÇÇ : Cochain G K n) (p q : ‚Ñ§) (hpq : p + n = q) :
    (z‚ÇÅ.comp z‚ÇÇ (zero_add n)).v p q hpq = z‚ÇÅ.v p p (add_zero p) ‚â´ z‚ÇÇ.v p q hpq :=
  comp_v z‚ÇÅ z‚ÇÇ (zero_add n) p p q (add_zero p) hpq

/-- The associativity of the composition of cochains. -/
lemma comp_assoc {n‚ÇÅ n‚ÇÇ n‚ÇÉ n‚ÇÅ‚ÇÇ n‚ÇÇ‚ÇÉ n‚ÇÅ‚ÇÇ‚ÇÉ : ‚Ñ§}
    (z‚ÇÅ : Cochain F G n‚ÇÅ) (z‚ÇÇ : Cochain G K n‚ÇÇ) (z‚ÇÉ : Cochain K L n‚ÇÉ)
    (h‚ÇÅ‚ÇÇ : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ) (h‚ÇÇ‚ÇÉ : n‚ÇÇ + n‚ÇÉ = n‚ÇÇ‚ÇÉ) (h‚ÇÅ‚ÇÇ‚ÇÉ : n‚ÇÅ + n‚ÇÇ + n‚ÇÉ = n‚ÇÅ‚ÇÇ‚ÇÉ) :
    (z‚ÇÅ.comp z‚ÇÇ h‚ÇÅ‚ÇÇ).comp z‚ÇÉ (show n‚ÇÅ‚ÇÇ + n‚ÇÉ = n‚ÇÅ‚ÇÇ‚ÇÉ by rw [‚Üê h‚ÇÅ‚ÇÇ, h‚ÇÅ‚ÇÇ‚ÇÉ]) =
      z‚ÇÅ.comp (z‚ÇÇ.comp z‚ÇÉ h‚ÇÇ‚ÇÉ) (by rw [‚Üê h‚ÇÇ‚ÇÉ, ‚Üê h‚ÇÅ‚ÇÇ‚ÇÉ, add_assoc]) := by
  substs h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÇ‚ÇÉ
  ext p q hpq
  rw [comp_v _ _ rfl p (p + n‚ÇÅ + n‚ÇÇ) q (add_assoc _ _ _).symm (by omega),
    comp_v z‚ÇÅ z‚ÇÇ rfl p (p + n‚ÇÅ) (p + n‚ÇÅ + n‚ÇÇ) (by omega) (by omega),
    comp_v z‚ÇÅ (z‚ÇÇ.comp z‚ÇÉ rfl) (add_assoc n‚ÇÅ n‚ÇÇ n‚ÇÉ).symm p (p + n‚ÇÅ) q (by omega) (by omega),
    comp_v z‚ÇÇ z‚ÇÉ rfl (p + n‚ÇÅ) (p + n‚ÇÅ + n‚ÇÇ) q (by omega) (by omega), assoc]

/-! The formulation of the associativity of the composition of cochains given by the
lemma `comp_assoc` often requires a careful selection of degrees with good definitional
properties. In a few cases, like when one of the three cochains is a `0`-cochain,
there are better choices, which provides the following simplification lemmas. -/

@[simp]
lemma comp_assoc_of_first_is_zero_cochain {n‚ÇÇ n‚ÇÉ n‚ÇÇ‚ÇÉ : ‚Ñ§}
    (z‚ÇÅ : Cochain F G 0) (z‚ÇÇ : Cochain G K n‚ÇÇ) (z‚ÇÉ : Cochain K L n‚ÇÉ)
    (h‚ÇÇ‚ÇÉ : n‚ÇÇ + n‚ÇÉ = n‚ÇÇ‚ÇÉ) :
    (z‚ÇÅ.comp z‚ÇÇ (zero_add n‚ÇÇ)).comp z‚ÇÉ h‚ÇÇ‚ÇÉ = z‚ÇÅ.comp (z‚ÇÇ.comp z‚ÇÉ h‚ÇÇ‚ÇÉ) (zero_add n‚ÇÇ‚ÇÉ) :=
  comp_assoc _ _ _ _ _ (by omega)

@[simp]
lemma comp_assoc_of_second_is_zero_cochain {n‚ÇÅ n‚ÇÉ n‚ÇÅ‚ÇÉ : ‚Ñ§}
    (z‚ÇÅ : Cochain F G n‚ÇÅ) (z‚ÇÇ : Cochain G K 0) (z‚ÇÉ : Cochain K L n‚ÇÉ) (h‚ÇÅ‚ÇÉ : n‚ÇÅ + n‚ÇÉ = n‚ÇÅ‚ÇÉ) :
    (z‚ÇÅ.comp z‚ÇÇ (add_zero n‚ÇÅ)).comp z‚ÇÉ h‚ÇÅ‚ÇÉ = z‚ÇÅ.comp (z‚ÇÇ.comp z‚ÇÉ (zero_add n‚ÇÉ)) h‚ÇÅ‚ÇÉ :=
  comp_assoc _ _ _ _ _ (by omega)

@[simp]
lemma comp_assoc_of_third_is_zero_cochain {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§}
    (z‚ÇÅ : Cochain F G n‚ÇÅ) (z‚ÇÇ : Cochain G K n‚ÇÇ) (z‚ÇÉ : Cochain K L 0) (h‚ÇÅ‚ÇÇ : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ) :
    (z‚ÇÅ.comp z‚ÇÇ h‚ÇÅ‚ÇÇ).comp z‚ÇÉ (add_zero n‚ÇÅ‚ÇÇ) = z‚ÇÅ.comp (z‚ÇÇ.comp z‚ÇÉ (add_zero n‚ÇÇ)) h‚ÇÅ‚ÇÇ :=
  comp_assoc _ _ _ _ _ (by omega)

@[simp]
lemma comp_assoc_of_second_degree_eq_neg_third_degree {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§}
    (z‚ÇÅ : Cochain F G n‚ÇÅ) (z‚ÇÇ : Cochain G K (-n‚ÇÇ)) (z‚ÇÉ : Cochain K L n‚ÇÇ) (h‚ÇÅ‚ÇÇ : n‚ÇÅ + (-n‚ÇÇ) = n‚ÇÅ‚ÇÇ) :
    (z‚ÇÅ.comp z‚ÇÇ h‚ÇÅ‚ÇÇ).comp z‚ÇÉ
      (show n‚ÇÅ‚ÇÇ + n‚ÇÇ = n‚ÇÅ by rw [‚Üê h‚ÇÅ‚ÇÇ, add_assoc, neg_add_cancel, add_zero]) =
      z‚ÇÅ.comp (z‚ÇÇ.comp z‚ÇÉ (neg_add_cancel n‚ÇÇ)) (add_zero n‚ÇÅ) :=
  comp_assoc _ _ _ _ _ (by omega)

@[simp]
protected lemma zero_comp {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÇ : Cochain G K n‚ÇÇ)
    (h : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ) : (0 : Cochain F G n‚ÇÅ).comp z‚ÇÇ h = 0 := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by omega), zero_v, zero_comp]

@[simp]
protected lemma add_comp {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ z‚ÇÅ' : Cochain F G n‚ÇÅ) (z‚ÇÇ : Cochain G K n‚ÇÇ)
    (h : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ) : (z‚ÇÅ + z‚ÇÅ').comp z‚ÇÇ h = z‚ÇÅ.comp z‚ÇÇ h + z‚ÇÅ'.comp z‚ÇÇ h := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by omega), add_v, add_comp]

@[simp]
protected lemma sub_comp {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ z‚ÇÅ' : Cochain F G n‚ÇÅ) (z‚ÇÇ : Cochain G K n‚ÇÇ)
    (h : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ) : (z‚ÇÅ - z‚ÇÅ').comp z‚ÇÇ h = z‚ÇÅ.comp z‚ÇÇ h - z‚ÇÅ'.comp z‚ÇÇ h := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by omega), sub_v, sub_comp]

@[simp]
protected lemma neg_comp {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ : Cochain F G n‚ÇÅ) (z‚ÇÇ : Cochain G K n‚ÇÇ)
    (h : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ) : (-z‚ÇÅ).comp z‚ÇÇ h = -z‚ÇÅ.comp z‚ÇÇ h := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by omega), neg_v, neg_comp]

@[simp]
protected lemma smul_comp {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (k : R) (z‚ÇÅ : Cochain F G n‚ÇÅ) (z‚ÇÇ : Cochain G K n‚ÇÇ)
    (h : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ) : (k ‚Ä¢ z‚ÇÅ).comp z‚ÇÇ h = k ‚Ä¢ (z‚ÇÅ.comp z‚ÇÇ h) := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by omega), smul_v, Linear.smul_comp]

@[simp]
lemma units_smul_comp {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (k : RÀ£) (z‚ÇÅ : Cochain F G n‚ÇÅ) (z‚ÇÇ : Cochain G K n‚ÇÇ)
    (h : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ) : (k ‚Ä¢ z‚ÇÅ).comp z‚ÇÇ h = k ‚Ä¢ (z‚ÇÅ.comp z‚ÇÇ h) := by
  apply Cochain.smul_comp

@[simp]
protected lemma id_comp {n : ‚Ñ§} (z‚ÇÇ : Cochain F G n) :
    (Cochain.ofHom (ùüô F)).comp z‚ÇÇ (zero_add n) = z‚ÇÇ := by
  ext p q hpq
  simp only [zero_cochain_comp_v, ofHom_v, HomologicalComplex.id_f, id_comp]

@[simp]
protected lemma comp_zero {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ : Cochain F G n‚ÇÅ)
    (h : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ) : z‚ÇÅ.comp (0 : Cochain G K n‚ÇÇ) h = 0 := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by omega), zero_v, comp_zero]

@[simp]
protected lemma comp_add {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ : Cochain F G n‚ÇÅ) (z‚ÇÇ z‚ÇÇ' : Cochain G K n‚ÇÇ)
    (h : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ) : z‚ÇÅ.comp (z‚ÇÇ + z‚ÇÇ') h = z‚ÇÅ.comp z‚ÇÇ h + z‚ÇÅ.comp z‚ÇÇ' h := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by omega), add_v, comp_add]

@[simp]
protected lemma comp_sub {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ : Cochain F G n‚ÇÅ) (z‚ÇÇ z‚ÇÇ' : Cochain G K n‚ÇÇ)
    (h : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ) : z‚ÇÅ.comp (z‚ÇÇ - z‚ÇÇ') h = z‚ÇÅ.comp z‚ÇÇ h - z‚ÇÅ.comp z‚ÇÇ' h := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by omega), sub_v, comp_sub]

@[simp]
protected lemma comp_neg {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ : Cochain F G n‚ÇÅ) (z‚ÇÇ : Cochain G K n‚ÇÇ)
    (h : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ) : z‚ÇÅ.comp (-z‚ÇÇ) h = -z‚ÇÅ.comp z‚ÇÇ h := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by omega), neg_v, comp_neg]

@[simp]
protected lemma comp_smul {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ : Cochain F G n‚ÇÅ) (k : R) (z‚ÇÇ : Cochain G K n‚ÇÇ)
    (h : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ) : z‚ÇÅ.comp (k ‚Ä¢ z‚ÇÇ) h = k ‚Ä¢ (z‚ÇÅ.comp z‚ÇÇ h) := by
  ext p q hpq
  simp only [comp_v _ _ h p _ q rfl (by omega), smul_v, Linear.comp_smul]

@[simp]
lemma comp_units_smul {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ : Cochain F G n‚ÇÅ) (k : RÀ£) (z‚ÇÇ : Cochain G K n‚ÇÇ)
    (h : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ) : z‚ÇÅ.comp (k ‚Ä¢ z‚ÇÇ) h = k ‚Ä¢ (z‚ÇÅ.comp z‚ÇÇ h) := by
  apply Cochain.comp_smul

@[simp]
protected lemma comp_id {n : ‚Ñ§} (z‚ÇÅ : Cochain F G n) :
    z‚ÇÅ.comp (Cochain.ofHom (ùüô G)) (add_zero n) = z‚ÇÅ := by
  ext p q hpq
  simp only [comp_zero_cochain_v, ofHom_v, HomologicalComplex.id_f, comp_id]

@[simp]
lemma ofHoms_comp (œÜ : ‚àÄ (p : ‚Ñ§), F.X p ‚ü∂ G.X p) (œà : ‚àÄ (p : ‚Ñ§), G.X p ‚ü∂ K.X p) :
    (ofHoms œÜ).comp (ofHoms œà) (zero_add 0) = ofHoms (fun p => œÜ p ‚â´ œà p) := by cat_disch

@[simp]
lemma ofHom_comp (f : F ‚ü∂ G) (g : G ‚ü∂ K) :
    ofHom (f ‚â´ g) = (ofHom f).comp (ofHom g) (zero_add 0) := by
  simp only [ofHom, HomologicalComplex.comp_f, ofHoms_comp]

variable (K)

/-- The differential on a cochain complex, as a cochain of degree `1`. -/
def diff : Cochain K K 1 := Cochain.mk (fun p q _ => K.d p q)

@[simp]
lemma diff_v (p q : ‚Ñ§) (hpq : p + 1 = q) : (diff K).v p q hpq = K.d p q := rfl

end Cochain

variable {F G}

/-- The differential on the complex of morphisms between cochain complexes. -/
def Œ¥ (z : Cochain F G n) : Cochain F G m :=
  Cochain.mk (fun p q hpq => z.v p (p + n) rfl ‚â´ G.d (p + n) q +
    m.negOnePow ‚Ä¢ F.d p (p + m - n) ‚â´ z.v (p + m - n) q (by rw [hpq, sub_add_cancel]))

/-! Similarly as for the composition of cochains, if `z : Cochain F G n`,
we usually need to carefully select intermediate indices with
good definitional properties in order to obtain a suitable expansion of the
morphisms which constitute `Œ¥ n m z : Cochain F G m` (when `n + 1 = m`, otherwise
it shall be zero). The basic equational lemma is `Œ¥_v` below. -/

lemma Œ¥_v (hnm : n + 1 = m) (z : Cochain F G n) (p q : ‚Ñ§) (hpq : p + m = q) (q‚ÇÅ q‚ÇÇ : ‚Ñ§)
    (hq‚ÇÅ : q‚ÇÅ = q - 1) (hq‚ÇÇ : p + 1 = q‚ÇÇ) : (Œ¥ n m z).v p q hpq =
    z.v p q‚ÇÅ (by rw [hq‚ÇÅ, ‚Üê hpq, ‚Üê hnm, ‚Üê add_assoc, add_sub_cancel_right]) ‚â´ G.d q‚ÇÅ q
      + m.negOnePow ‚Ä¢ F.d p q‚ÇÇ ‚â´ z.v q‚ÇÇ q
          (by rw [‚Üê hq‚ÇÇ, add_assoc, add_comm 1, hnm, hpq]) := by
  obtain rfl : q‚ÇÅ = p + n := by omega
  obtain rfl : q‚ÇÇ = p + m - n := by omega
  rfl

lemma Œ¥_shape (hnm : ¬¨ n + 1 = m) (z : Cochain F G n) : Œ¥ n m z = 0 := by
  ext p q hpq
  dsimp only [Œ¥]
  rw [Cochain.mk_v, Cochain.zero_v, F.shape, G.shape, comp_zero, zero_add, zero_comp, smul_zero]
  all_goals
    simp only [ComplexShape.up_Rel]
    exact fun _ => hnm (by omega)

variable (F G) (R)

/-- The differential on the complex of morphisms between cochain complexes, as a linear map. -/
@[simps!]
def Œ¥_hom : Cochain F G n ‚Üí‚Çó[R] Cochain F G m where
  toFun := Œ¥ n m
  map_add' Œ± Œ≤ := by
    by_cases h : n + 1 = m
    ¬∑ ext p q hpq
      dsimp
      simp only [Œ¥_v n m h _ p q hpq _ _ rfl rfl, Cochain.add_v, add_comp, comp_add, smul_add]
      abel
    ¬∑ simp only [Œ¥_shape _ _ h, add_zero]
  map_smul' r a := by
    by_cases h : n + 1 = m
    ¬∑ ext p q hpq
      dsimp
      simp only [Œ¥_v n m h _ p q hpq _ _ rfl rfl, Cochain.smul_v, Linear.comp_smul,
        Linear.smul_comp, smul_add, smul_comm m.negOnePow r]
    ¬∑ simp only [Œ¥_shape _ _ h, smul_zero]

variable {F G R}

@[simp] lemma Œ¥_add (z‚ÇÅ z‚ÇÇ : Cochain F G n) : Œ¥ n m (z‚ÇÅ + z‚ÇÇ) = Œ¥ n m z‚ÇÅ + Œ¥ n m z‚ÇÇ :=
  (Œ¥_hom ‚Ñ§ F G n m).map_add z‚ÇÅ z‚ÇÇ

@[simp] lemma Œ¥_sub (z‚ÇÅ z‚ÇÇ : Cochain F G n) : Œ¥ n m (z‚ÇÅ - z‚ÇÇ) = Œ¥ n m z‚ÇÅ - Œ¥ n m z‚ÇÇ :=
  (Œ¥_hom ‚Ñ§ F G n m).map_sub z‚ÇÅ z‚ÇÇ

@[simp] lemma Œ¥_zero : Œ¥ n m (0 : Cochain F G n) = 0 := (Œ¥_hom ‚Ñ§ F G n m).map_zero

@[simp] lemma Œ¥_neg (z : Cochain F G n) : Œ¥ n m (-z) = - Œ¥ n m z :=
  (Œ¥_hom ‚Ñ§ F G n m).map_neg z

@[simp] lemma Œ¥_smul (k : R) (z : Cochain F G n) : Œ¥ n m (k ‚Ä¢ z) = k ‚Ä¢ Œ¥ n m z :=
  (Œ¥_hom R F G n m).map_smul k z

@[simp] lemma Œ¥_units_smul (k : RÀ£) (z : Cochain F G n) : Œ¥ n m (k ‚Ä¢ z) = k ‚Ä¢ Œ¥ n m z :=
  Œ¥_smul ..

lemma Œ¥_Œ¥ (n‚ÇÄ n‚ÇÅ n‚ÇÇ : ‚Ñ§) (z : Cochain F G n‚ÇÄ) : Œ¥ n‚ÇÅ n‚ÇÇ (Œ¥ n‚ÇÄ n‚ÇÅ z) = 0 := by
  by_cases h‚ÇÅ‚ÇÇ : n‚ÇÅ + 1 = n‚ÇÇ; swap
  ¬∑ rw [Œ¥_shape _ _ h‚ÇÅ‚ÇÇ]
  by_cases h‚ÇÄ‚ÇÅ : n‚ÇÄ + 1 = n‚ÇÅ; swap
  ¬∑ rw [Œ¥_shape _ _ h‚ÇÄ‚ÇÅ, Œ¥_zero]
  ext p q hpq
  dsimp
  simp only [Œ¥_v n‚ÇÅ n‚ÇÇ h‚ÇÅ‚ÇÇ _ p q hpq _ _ rfl rfl,
    Œ¥_v n‚ÇÄ n‚ÇÅ h‚ÇÄ‚ÇÅ z p (q-1) (by omega) (q-2) _ (by omega) rfl,
    Œ¥_v n‚ÇÄ n‚ÇÅ h‚ÇÄ‚ÇÅ z (p+1) q (by omega) _ (p+2) rfl (by omega),
    ‚Üê h‚ÇÅ‚ÇÇ, Int.negOnePow_succ, add_comp, assoc,
    HomologicalComplex.d_comp_d, comp_zero, zero_add, comp_add,
    HomologicalComplex.d_comp_d_assoc, zero_comp, smul_zero,
    add_zero, add_neg_cancel, Units.neg_smul,
    Linear.units_smul_comp, Linear.comp_units_smul]

lemma Œ¥_comp {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ : Cochain F G n‚ÇÅ) (z‚ÇÇ : Cochain G K n‚ÇÇ) (h : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ)
    (m‚ÇÅ m‚ÇÇ m‚ÇÅ‚ÇÇ : ‚Ñ§) (h‚ÇÅ‚ÇÇ : n‚ÇÅ‚ÇÇ + 1 = m‚ÇÅ‚ÇÇ) (h‚ÇÅ : n‚ÇÅ + 1 = m‚ÇÅ) (h‚ÇÇ : n‚ÇÇ + 1 = m‚ÇÇ) :
    Œ¥ n‚ÇÅ‚ÇÇ m‚ÇÅ‚ÇÇ (z‚ÇÅ.comp z‚ÇÇ h) = z‚ÇÅ.comp (Œ¥ n‚ÇÇ m‚ÇÇ z‚ÇÇ) (by rw [‚Üê h‚ÇÅ‚ÇÇ, ‚Üê h‚ÇÇ, ‚Üê h, add_assoc]) +
      n‚ÇÇ.negOnePow ‚Ä¢ (Œ¥ n‚ÇÅ m‚ÇÅ z‚ÇÅ).comp z‚ÇÇ
        (by rw [‚Üê h‚ÇÅ‚ÇÇ, ‚Üê h‚ÇÅ, ‚Üê h, add_assoc, add_comm 1, add_assoc]) := by
  subst h‚ÇÅ‚ÇÇ h‚ÇÅ h‚ÇÇ h
  ext p q hpq
  dsimp
  rw [z‚ÇÅ.comp_v _ (add_assoc n‚ÇÅ n‚ÇÇ 1).symm p _ q rfl (by omega),
    Cochain.comp_v _ _ (show n‚ÇÅ + 1 + n‚ÇÇ = n‚ÇÅ + n‚ÇÇ + 1 by omega) p (p+n‚ÇÅ+1) q
      (by omega) (by omega),
    Œ¥_v (n‚ÇÅ + n‚ÇÇ) _ rfl (z‚ÇÅ.comp z‚ÇÇ rfl) p q hpq (p + n‚ÇÅ + n‚ÇÇ) _ (by omega) rfl,
    z‚ÇÅ.comp_v z‚ÇÇ rfl p _ _ rfl rfl,
    z‚ÇÅ.comp_v z‚ÇÇ rfl (p+1) (p+n‚ÇÅ+1) q (by omega) (by omega),
    Œ¥_v n‚ÇÇ (n‚ÇÇ+1) rfl z‚ÇÇ (p+n‚ÇÅ) q (by omega) (p+n‚ÇÅ+n‚ÇÇ) _ (by omega) rfl,
    Œ¥_v n‚ÇÅ (n‚ÇÅ+1) rfl z‚ÇÅ p (p+n‚ÇÅ+1) (by omega) (p+n‚ÇÅ) _ (by omega) rfl]
  simp only [assoc, comp_add, add_comp, Int.negOnePow_succ, Int.negOnePow_add n‚ÇÅ n‚ÇÇ,
    Units.neg_smul, comp_neg, neg_comp, smul_neg, smul_smul, Linear.units_smul_comp,
    mul_comm n‚ÇÅ.negOnePow n‚ÇÇ.negOnePow, Linear.comp_units_smul, smul_add]
  abel

lemma Œ¥_zero_cochain_comp {n‚ÇÇ : ‚Ñ§} (z‚ÇÅ : Cochain F G 0) (z‚ÇÇ : Cochain G K n‚ÇÇ)
    (m‚ÇÇ : ‚Ñ§) (h‚ÇÇ : n‚ÇÇ + 1 = m‚ÇÇ) :
    Œ¥ n‚ÇÇ m‚ÇÇ (z‚ÇÅ.comp z‚ÇÇ (zero_add n‚ÇÇ)) =
      z‚ÇÅ.comp (Œ¥ n‚ÇÇ m‚ÇÇ z‚ÇÇ) (zero_add m‚ÇÇ) +
      n‚ÇÇ.negOnePow ‚Ä¢ ((Œ¥ 0 1 z‚ÇÅ).comp z‚ÇÇ (by rw [add_comm, h‚ÇÇ])) :=
  Œ¥_comp z‚ÇÅ z‚ÇÇ (zero_add n‚ÇÇ) 1 m‚ÇÇ m‚ÇÇ h‚ÇÇ (zero_add 1) h‚ÇÇ

lemma Œ¥_comp_zero_cochain {n‚ÇÅ : ‚Ñ§} (z‚ÇÅ : Cochain F G n‚ÇÅ) (z‚ÇÇ : Cochain G K 0)
    (m‚ÇÅ : ‚Ñ§) (h‚ÇÅ : n‚ÇÅ + 1 = m‚ÇÅ) :
    Œ¥ n‚ÇÅ m‚ÇÅ (z‚ÇÅ.comp z‚ÇÇ (add_zero n‚ÇÅ)) =
      z‚ÇÅ.comp (Œ¥ 0 1 z‚ÇÇ) h‚ÇÅ + (Œ¥ n‚ÇÅ m‚ÇÅ z‚ÇÅ).comp z‚ÇÇ (add_zero m‚ÇÅ) := by
  simp only [Œ¥_comp z‚ÇÅ z‚ÇÇ (add_zero n‚ÇÅ) m‚ÇÅ 1 m‚ÇÅ h‚ÇÅ h‚ÇÅ (zero_add 1), one_smul,
    Int.negOnePow_zero]

@[simp]
lemma Œ¥_zero_cochain_v (z : Cochain F G 0) (p q : ‚Ñ§) (hpq : p + 1 = q) :
    (Œ¥ 0 1 z).v p q hpq = z.v p p (add_zero p) ‚â´ G.d p q - F.d p q ‚â´ z.v q q (add_zero q) := by
  simp only [Œ¥_v 0 1 (zero_add 1) z p q hpq p q (by omega) hpq, Int.negOnePow_one, Units.neg_smul,
    one_smul, sub_eq_add_neg]

@[simp]
lemma Œ¥_ofHom {p : ‚Ñ§} (œÜ : F ‚ü∂ G) : Œ¥ 0 p (Cochain.ofHom œÜ) = 0 := by
  by_cases h : p = 1
  ¬∑ subst h
    ext
    simp
  ¬∑ rw [Œ¥_shape]
    omega

@[simp]
lemma Œ¥_ofHomotopy {œÜ‚ÇÅ œÜ‚ÇÇ : F ‚ü∂ G} (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) :
    Œ¥ (-1) 0 (Cochain.ofHomotopy h) = Cochain.ofHom œÜ‚ÇÅ - Cochain.ofHom œÜ‚ÇÇ := by
  ext p
  have eq := h.comm p
  rw [dNext_eq h.hom (show (ComplexShape.up ‚Ñ§).Rel p (p+1) by simp),
    prevD_eq h.hom (show (ComplexShape.up ‚Ñ§).Rel (p-1) p by simp)] at eq
  rw [Cochain.ofHomotopy, Œ¥_v (-1) 0 (neg_add_cancel 1) _ p p (add_zero p) (p-1) (p+1) rfl rfl]
  simp only [Cochain.mk_v, one_smul, Int.negOnePow_zero, Cochain.sub_v, Cochain.ofHom_v, eq]
  abel

lemma Œ¥_neg_one_cochain (z : Cochain F G (-1)) :
    Œ¥ (-1) 0 z = Cochain.ofHom (Homotopy.nullHomotopicMap'
      (fun i j hij => z.v i j (by dsimp at hij; rw [‚Üê hij, add_neg_cancel_right]))) := by
  ext p
  rw [Œ¥_v (-1) 0 (neg_add_cancel 1) _ p p (add_zero p) (p-1) (p+1) rfl rfl]
  simp only [one_smul, Cochain.ofHom_v, Int.negOnePow_zero]
  rw [Homotopy.nullHomotopicMap'_f (show (ComplexShape.up ‚Ñ§).Rel (p-1) p by simp)
    (show (ComplexShape.up ‚Ñ§).Rel p (p+1) by simp)]
  abel

end HomComplex

variable (F G)

open HomComplex

/-- The cochain complex of homomorphisms between two cochain complexes `F` and `G`.
In degree `n : ‚Ñ§`, it consists of the abelian group `HomComplex.Cochain F G n`. -/
@[simps! X d_hom_apply]
def HomComplex : CochainComplex AddCommGrp ‚Ñ§ where
  X i := AddCommGrp.of (Cochain F G i)
  d i j := AddCommGrp.ofHom (Œ¥_hom ‚Ñ§ F G i j)
  shape _ _ hij := by ext; simp [Œ¥_shape _ _ hij]
  d_comp_d' _ _ _ _ _  := by ext; simp [Œ¥_Œ¥]

namespace HomComplex

/-- The subgroup of cocycles in `Cochain F G n`. -/
def cocycle : AddSubgroup (Cochain F G n) :=
  AddMonoidHom.ker (Œ¥_hom ‚Ñ§ F G n (n + 1)).toAddMonoidHom

/-- The type of `n`-cocycles, as a subtype of `Cochain F G n`. -/
def Cocycle : Type v := cocycle F G n

instance : AddCommGroup (Cocycle F G n) := by
  dsimp only [Cocycle]
  infer_instance

namespace Cocycle

variable {F G}

lemma mem_iff (hnm : n + 1 = m) (z : Cochain F G n) :
    z ‚àà cocycle F G n ‚Üî Œ¥ n m z = 0 := by subst hnm; rfl

variable {n}

instance : Coe (Cocycle F G n) (Cochain F G n) where
  coe x := x.1

@[ext]
lemma ext (z‚ÇÅ z‚ÇÇ : Cocycle F G n) (h : (z‚ÇÅ : Cochain F G n) = z‚ÇÇ) : z‚ÇÅ = z‚ÇÇ :=
  Subtype.ext h

instance : SMul R (Cocycle F G n) where
  smul r z := ‚ü®r ‚Ä¢ z.1, by
    have hz := z.2
    rw [mem_iff n (n + 1) rfl] at hz ‚ä¢
    simp only [Œ¥_smul, hz, smul_zero]‚ü©

variable (F G n)

@[simp]
lemma coe_zero : (‚Üë(0 : Cocycle F G n) : Cochain F G n) = 0 := by rfl

variable {F G n}

@[simp]
lemma coe_add (z‚ÇÅ z‚ÇÇ : Cocycle F G n) :
    (‚Üë(z‚ÇÅ + z‚ÇÇ) : Cochain F G n) = (z‚ÇÅ : Cochain F G n) + (z‚ÇÇ : Cochain F G n) := rfl

@[simp]
lemma coe_neg (z : Cocycle F G n) :
    (‚Üë(-z) : Cochain F G n) = -(z : Cochain F G n) := rfl

@[simp]
lemma coe_smul (z : Cocycle F G n) (x : R) :
    (‚Üë(x ‚Ä¢ z) : Cochain F G n) = x ‚Ä¢ (z : Cochain F G n) := rfl

@[simp]
lemma coe_units_smul (z : Cocycle F G n) (x : RÀ£) :
    (‚Üë(x ‚Ä¢ z) : Cochain F G n) = x ‚Ä¢ (z : Cochain F G n) := rfl

@[simp]
lemma coe_sub (z‚ÇÅ z‚ÇÇ : Cocycle F G n) :
    (‚Üë(z‚ÇÅ - z‚ÇÇ) : Cochain F G n) = (z‚ÇÅ : Cochain F G n) - (z‚ÇÇ : Cochain F G n) := rfl

instance : Module R (Cocycle F G n) where
  one_smul _ := by aesop
  mul_smul _ _ _ := by ext; dsimp; rw [smul_smul]
  smul_zero _ := by aesop
  smul_add _ _ _ := by aesop
  add_smul _ _ _ := by ext; dsimp; rw [add_smul]
  zero_smul := by aesop

/-- Constructor for `Cocycle F G n`, taking as inputs `z : Cochain F G n`, an integer
`m : ‚Ñ§` such that `n + 1 = m`, and the relation `Œ¥ n m z = 0`. -/
@[simps]
def mk (z : Cochain F G n) (m : ‚Ñ§) (hnm : n + 1 = m) (h : Œ¥ n m z = 0) : Cocycle F G n :=
  ‚ü®z, by simpa only [mem_iff n m hnm z] using h‚ü©

@[simp]
lemma Œ¥_eq_zero {n : ‚Ñ§} (z : Cocycle F G n) (m : ‚Ñ§) : Œ¥ n m (z : Cochain F G n) = 0 := by
  by_cases h : n + 1 = m
  ¬∑ rw [‚Üê mem_iff n m h]
    exact z.2
  ¬∑ exact Œ¥_shape n m h _

/-- The `0`-cocycle associated to a morphism in `CochainComplex C ‚Ñ§`. -/
@[simps!]
def ofHom (œÜ : F ‚ü∂ G) : Cocycle F G 0 := mk (Cochain.ofHom œÜ) 1 (zero_add 1) (by simp)

/-- The morphism in `CochainComplex C ‚Ñ§` associated to a `0`-cocycle. -/
@[simps]
def homOf (z : Cocycle F G 0) : F ‚ü∂ G where
  f i := (z : Cochain _ _ _).v i i (add_zero i)
  comm' := by
    rintro i j rfl
    rcases z with ‚ü®z, hz‚ü©
    dsimp
    rw [mem_iff 0 1 (zero_add 1)] at hz
    simpa only [Œ¥_zero_cochain_v, Cochain.zero_v, sub_eq_zero]
      using Cochain.congr_v hz i (i + 1) rfl

@[simp]
lemma homOf_ofHom_eq_self (œÜ : F ‚ü∂ G) : homOf (ofHom œÜ) = œÜ := by cat_disch

@[simp]
lemma ofHom_homOf_eq_self (z : Cocycle F G 0) : ofHom (homOf z) = z := by cat_disch

@[simp]
lemma cochain_ofHom_homOf_eq_coe (z : Cocycle F G 0) :
    Cochain.ofHom (homOf z) = (z : Cochain F G 0) := by
  simpa only [Cocycle.ext_iff] using ofHom_homOf_eq_self z

variable (F G)

/-- The additive equivalence between morphisms in `CochainComplex C ‚Ñ§` and `0`-cocycles. -/
@[simps]
def equivHom : (F ‚ü∂ G) ‚âÉ+ Cocycle F G 0 where
  toFun := ofHom
  invFun := homOf
  left_inv := homOf_ofHom_eq_self
  right_inv := ofHom_homOf_eq_self
  map_add' := by cat_disch

variable (K)

/-- The `1`-cocycle given by the differential on a cochain complex. -/
@[simps!]
def diff : Cocycle K K 1 :=
  Cocycle.mk (Cochain.diff K) 2 rfl (by
    ext p q hpq
    simp only [Cochain.zero_v, Œ¥_v 1 2 rfl _ p q hpq _ _ rfl rfl, Cochain.diff_v,
      HomologicalComplex.d_comp_d, smul_zero, add_zero])

end Cocycle

variable {F G}

@[simp]
lemma Œ¥_comp_zero_cocycle {n : ‚Ñ§} (z‚ÇÅ : Cochain F G n) (z‚ÇÇ : Cocycle G K 0) (m : ‚Ñ§) :
    Œ¥ n m (z‚ÇÅ.comp z‚ÇÇ.1 (add_zero n)) =
      (Œ¥ n m z‚ÇÅ).comp z‚ÇÇ.1 (add_zero m) := by
  by_cases hnm : n + 1 = m
  ¬∑ simp [Œ¥_comp_zero_cochain _ _ _ hnm]
  ¬∑ simp [Œ¥_shape _ _ hnm]

@[simp]
lemma Œ¥_comp_ofHom {n : ‚Ñ§} (z‚ÇÅ : Cochain F G n) (f : G ‚ü∂ K) (m : ‚Ñ§) :
    Œ¥ n m (z‚ÇÅ.comp (Cochain.ofHom f) (add_zero n)) =
      (Œ¥ n m z‚ÇÅ).comp (Cochain.ofHom f) (add_zero m) := by
  rw [‚Üê Cocycle.ofHom_coe, Œ¥_comp_zero_cocycle]


@[simp]
lemma Œ¥_zero_cocycle_comp {n : ‚Ñ§} (z‚ÇÅ : Cocycle F G 0) (z‚ÇÇ : Cochain G K n) (m : ‚Ñ§) :
    Œ¥ n m (z‚ÇÅ.1.comp z‚ÇÇ (zero_add n)) =
      z‚ÇÅ.1.comp (Œ¥ n m z‚ÇÇ) (zero_add m) := by
  by_cases hnm : n + 1 = m
  ¬∑ simp [Œ¥_zero_cochain_comp _ _ _ hnm]
  ¬∑ simp [Œ¥_shape _ _ hnm]

@[simp]
lemma Œ¥_ofHom_comp {n : ‚Ñ§} (f : F ‚ü∂ G) (z : Cochain G K n) (m : ‚Ñ§) :
    Œ¥ n m ((Cochain.ofHom f).comp z (zero_add n)) =
      (Cochain.ofHom f).comp (Œ¥ n m z) (zero_add m) := by
  rw [‚Üê Cocycle.ofHom_coe, Œ¥_zero_cocycle_comp]

namespace Cochain

/-- Given two morphisms of complexes `œÜ‚ÇÅ œÜ‚ÇÇ : F ‚ü∂ G`, the datum of an homotopy between `œÜ‚ÇÅ` and
`œÜ‚ÇÇ` is equivalent to the datum of a `1`-cochain `z` such that `Œ¥ (-1) 0 z` is the difference
of the zero cochains associated to `œÜ‚ÇÇ` and `œÜ‚ÇÅ`. -/
@[simps]
def equivHomotopy (œÜ‚ÇÅ œÜ‚ÇÇ : F ‚ü∂ G) :
    Homotopy œÜ‚ÇÅ œÜ‚ÇÇ ‚âÉ
      { z : Cochain F G (-1) // Cochain.ofHom œÜ‚ÇÅ = Œ¥ (-1) 0 z + Cochain.ofHom œÜ‚ÇÇ } where
  toFun ho := ‚ü®Cochain.ofHomotopy ho, by simp only [Œ¥_ofHomotopy, sub_add_cancel]‚ü©
  invFun z :=
    { hom := fun i j => if hij : i + (-1) = j then z.1.v i j hij else 0
      zero := fun i j (hij : j + 1 ‚â† i) => dif_neg (fun _ => hij (by omega))
      comm := fun p => by
        have eq := Cochain.congr_v z.2 p p (add_zero p)
        have h‚ÇÅ : (ComplexShape.up ‚Ñ§).Rel (p - 1) p := by simp
        have h‚ÇÇ : (ComplexShape.up ‚Ñ§).Rel p (p + 1) := by simp
        simp only [Œ¥_neg_one_cochain, Cochain.ofHom_v, ComplexShape.up_Rel, Cochain.add_v,
          Homotopy.nullHomotopicMap'_f h‚ÇÅ h‚ÇÇ] at eq
        rw [dNext_eq _ h‚ÇÇ, prevD_eq _ h‚ÇÅ, eq, dif_pos, dif_pos] }
  left_inv := fun ho => by
    ext i j
    dsimp
    split_ifs with h
    ¬∑ rfl
    ¬∑ rw [ho.zero i j (fun h' => h (by dsimp at h'; omega))]
  right_inv := fun z => by
    ext p q hpq
    dsimp [Cochain.ofHomotopy]
    rw [dif_pos hpq]

@[simp]
lemma equivHomotopy_apply_of_eq {œÜ‚ÇÅ œÜ‚ÇÇ : F ‚ü∂ G} (h : œÜ‚ÇÅ = œÜ‚ÇÇ) :
    (equivHomotopy _ _ (Homotopy.ofEq h)).1 = 0 := rfl

lemma ofHom_injective {f‚ÇÅ f‚ÇÇ : F ‚ü∂ G} (h : ofHom f‚ÇÅ = ofHom f‚ÇÇ) : f‚ÇÅ = f‚ÇÇ :=
  (Cocycle.equivHom F G).injective (by ext1; exact h)

end Cochain

section

variable {n} {D : Type*} [Category D] [Preadditive D] (z z' : Cochain K L n) (f : K ‚ü∂ L)
  (Œ¶ : C ‚•§ D) [Œ¶.Additive]

namespace Cochain

/-- If `Œ¶ : C ‚•§ D` is an additive functor, a cochain `z : Cochain K L n` between
cochain complexes in `C` can be mapped to a cochain between the cochain complexes
in `D` obtained by applying the functor
`Œ¶.mapHomologicalComplex _ : CochainComplex C ‚Ñ§ ‚•§ CochainComplex D ‚Ñ§`. -/
def map : Cochain ((Œ¶.mapHomologicalComplex _).obj K) ((Œ¶.mapHomologicalComplex _).obj L) n :=
  Cochain.mk (fun p q hpq => Œ¶.map (z.v p q hpq))

@[simp]
lemma map_v (p q : ‚Ñ§) (hpq : p + n = q) : (z.map Œ¶).v p q hpq = Œ¶.map (z.v p q hpq) := rfl

@[simp]
protected lemma map_add : (z + z').map Œ¶ = z.map Œ¶ + z'.map Œ¶ := by cat_disch

@[simp]
protected lemma map_neg : (-z).map Œ¶ = -z.map Œ¶ := by cat_disch

@[simp]
protected lemma map_sub : (z - z').map Œ¶ = z.map Œ¶ - z'.map Œ¶ := by cat_disch

variable (K L n)

@[simp]
protected lemma map_zero : (0 : Cochain K L n).map Œ¶ = 0 := by cat_disch

@[simp]
lemma map_comp {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ : Cochain F G n‚ÇÅ) (z‚ÇÇ : Cochain G K n‚ÇÇ) (h : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ)
    (Œ¶ : C ‚•§ D) [Œ¶.Additive] :
    (Cochain.comp z‚ÇÅ z‚ÇÇ h).map Œ¶ = Cochain.comp (z‚ÇÅ.map Œ¶) (z‚ÇÇ.map Œ¶) h := by
  ext p q hpq
  dsimp
  simp only [map_v, comp_v _ _ h p _ q rfl (by omega), Œ¶.map_comp]

@[simp]
lemma map_ofHom :
    (Cochain.ofHom f).map Œ¶ = Cochain.ofHom ((Œ¶.mapHomologicalComplex _).map f) := by cat_disch

end Cochain

variable (n)

@[simp]
lemma Œ¥_map : Œ¥ n m (z.map Œ¶) = (Œ¥ n m z).map Œ¶ := by
  by_cases hnm : n + 1 = m
  ¬∑ ext p q hpq
    dsimp
    simp only [Œ¥_v n m hnm _ p q hpq (q-1) (p+1) rfl rfl,
      Functor.map_add, Functor.map_comp, Functor.map_units_smul,
      Cochain.map_v, Functor.mapHomologicalComplex_obj_d]
  ¬∑ simp only [Œ¥_shape _ _ hnm, Cochain.map_zero]

end

end HomComplex

end CochainComplex
