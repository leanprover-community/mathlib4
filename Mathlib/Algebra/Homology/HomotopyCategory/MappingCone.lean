/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.HomotopyCategory.HomComplex
import Mathlib.Algebra.Homology.HomotopyCofiber

/-! # The mapping cone of a morphism of cochain complexes

In this file, we study the homotopy cofiber `HomologicalComplex.homotopyCofiber`
of a morphism `Ï† : F âŸ¶ G` of cochain complexes indexed by `â„¤`. In this case,
we redefine it as `CochainComplex.mappingCone Ï†`. The API involves definitions
- `mappingCone.inl Ï† : Cochain F (mappingCone Ï†) (-1)`,
- `mappingCone.inr Ï† : G âŸ¶ mappingCone Ï†`,
- `mappingCone.fst Ï† : Cocycle (mappingCone Ï†) F 1` and
- `mappingCone.snd Ï† : Cochain (mappingCone Ï†) G 0`.

-/

open CategoryTheory Limits

variable {C : Type*} [Category C] [Preadditive C]

namespace CochainComplex

open HomologicalComplex

section

variable {Î¹ : Type*} [AddRightCancelSemigroup Î¹] [One Î¹]
    {F G : CochainComplex C Î¹} (Ï† : F âŸ¶ G)

instance [âˆ€ p, HasBinaryBiproduct (F.X (p + 1)) (G.X p)] :
    HasHomotopyCofiber Ï† where
  hasBinaryBiproduct := by
    rintro i _ rfl
    infer_instance

end

variable {F G : CochainComplex C â„¤} (Ï† : F âŸ¶ G)

--instance : DecidableRel (ComplexShape.up â„¤).Rel := fun _ _ => by dsimp; infer_instance

variable [HasHomotopyCofiber Ï†]

/-- The mapping cone of a morphism of cochain complexes indexed by `â„¤`. -/
noncomputable def mappingCone := homotopyCofiber Ï†

namespace mappingCone

open HomComplex

/-- The left inclusion in the mapping cone, as a cochain of degree `-1`. -/
noncomputable def inl : Cochain F (mappingCone Ï†) (-1) :=
  Cochain.mk (fun p q hpq => homotopyCofiber.inlX Ï† p q  (by dsimp; linarith))

/-- The right inclusion in the mapping cone. -/
noncomputable def inr : G âŸ¶ mappingCone Ï† := homotopyCofiber.inr Ï†

/-- The first projection from the mapping cone, as a cocyle of degree `1`. -/
noncomputable def fst : Cocycle (mappingCone Ï†) F 1 :=
  Cocycle.mk (Cochain.mk (fun p q hpq => homotopyCofiber.fstX Ï† p q hpq)) 2 (by linarith) (by
    ext p _ rfl
    simp [Î´_v 1 2 (by linarith) _ p (p + 2) (by linarith) (p + 1) (p + 1) (by linarith) rfl,
      homotopyCofiber.d_fstX Ï† p (p + 1) (p + 2) rfl, mappingCone,
      show Int.negOnePow 2 = 1 by rfl])

/-- The second projection from the mapping cone, as a cochain of degree `0`. -/
noncomputable def snd : Cochain (mappingCone Ï†) G 0 :=
  Cochain.ofHoms (homotopyCofiber.sndX Ï†)

@[reassoc (attr := simp)]
lemma inl_v_fst_v (p q : â„¤) (hpq : q + 1 = p) :
    (inl Ï†).v p q (by rw [â† hpq, add_neg_cancel_right]) â‰«
      (fst Ï† : Cochain (mappingCone Ï†) F 1).v q p hpq = ğŸ™ _ := by
  simp [inl, fst]

@[reassoc (attr := simp)]
lemma inl_v_snd_v (p q : â„¤) (hpq : p + (-1) = q) :
    (inl Ï†).v p q hpq â‰« (snd Ï†).v q q (add_zero q) = 0 := by
  simp [inl, snd]

@[reassoc (attr := simp)]
lemma inr_f_fst_v (p q : â„¤) (hpq : p + 1 = q) :
    (inr Ï†).f p â‰« (fst Ï†).1.v p q hpq = 0 := by
  simp [inr, fst]

@[reassoc (attr := simp)]
lemma inr_f_snd_v (p : â„¤) :
    (inr Ï†).f p â‰« (snd Ï†).v p p (add_zero p) = ğŸ™ _ := by
  simp [inr, snd]

@[simp]
lemma inl_fst :
    (inl Ï†).comp (fst Ï†).1 (neg_add_self 1) = Cochain.ofHom (ğŸ™ F) := by
  ext p
  simp [Cochain.comp_v _ _ (neg_add_self 1) p (p-1) p rfl (by linarith)]

@[simp]
lemma inl_snd :
    (inl Ï†).comp (snd Ï†) (add_zero (-1)) = 0 := by
  ext p q hpq
  simp [Cochain.comp_v _ _ (add_zero (-1)) p q q (by linarith) (by linarith)]

@[simp]
lemma inr_fst :
    (Cochain.ofHom (inr Ï†)).comp (fst Ï†).1 (zero_add 1) = 0 := by
  ext p q hpq
  simp [Cochain.comp_v _ _ (zero_add 1) p p q (by linarith) (by linarith)]

@[simp]
lemma inr_snd :
    (Cochain.ofHom (inr Ï†)).comp (snd Ï†) (zero_add 0) = Cochain.ofHom (ğŸ™ G) := by aesop_cat

/-! In order to obtain identities of cochains involving `inl`, `inr`, `fst` and `snd`,
it is often convenient to use an `ext` lemma, and use simp lemmas like `inl_v_f_fst_v`,
but it is sometimes possible to get identities of cochains by using rewrites of
identities of cochains like `inl_fst`. Then, similarly as in category theory,
if we associate the compositions of cochains to the right as much as possible,
it is also interesting to have `reassoc` variants of lemmas, like `inl_fst_assoc`. -/

@[simp]
lemma inl_fst_assoc {K : CochainComplex C â„¤} {d e : â„¤} (Î³ : Cochain F K d) (he : 1 + d = e) :
    (inl Ï†).comp ((fst Ï†).1.comp Î³ he) (by rw [â† he, neg_add_cancel_left]) = Î³ := by
  rw [â† Cochain.comp_assoc _ _ _ (neg_add_self 1) (by linarith) (by linarith), inl_fst,
    Cochain.id_comp]

@[simp]
lemma inl_snd_assoc {K : CochainComplex C â„¤} {d e f : â„¤} (Î³ : Cochain G K d)
    (he : 0 + d = e) (hf : -1 + e = f) :
    (inl Ï†).comp ((snd Ï†).comp Î³ he) hf = 0 := by
  obtain rfl : e = d := by linarith
  rw [â† Cochain.comp_assoc_of_second_is_zero_cochain, inl_snd, Cochain.zero_comp]

@[simp]
lemma inr_fst_assoc {K : CochainComplex C â„¤} {d e f : â„¤} (Î³ : Cochain F K d)
    (he : 1 + d = e) (hf : 0 + e = f) :
    (Cochain.ofHom (inr Ï†)).comp ((fst Ï†).1.comp Î³ he) hf = 0 := by
  obtain rfl : e = f := by linarith
  rw [â† Cochain.comp_assoc_of_first_is_zero_cochain, inr_fst, Cochain.zero_comp]

@[simp]
lemma inr_snd_assoc {K : CochainComplex C â„¤} {d e : â„¤} (Î³ : Cochain G K d) (he : 0 + d = e) :
    (Cochain.ofHom (inr Ï†)).comp ((snd Ï†).comp Î³ he) (by simp only [â† he, zero_add]) = Î³ := by
  obtain rfl : d = e := by linarith
  rw [â† Cochain.comp_assoc_of_first_is_zero_cochain, inr_snd, Cochain.id_comp]

lemma ext_to (i j : â„¤) (hij : i + 1 = j) {A : C} {f g : A âŸ¶ (mappingCone Ï†).X i}
    (hâ‚ : f â‰« (fst Ï†).1.v i j hij = g â‰« (fst Ï†).1.v i j hij)
    (hâ‚‚ : f â‰« (snd Ï†).v i i (add_zero i) = g â‰« (snd Ï†).v i i (add_zero i)) :
    f = g :=
  homotopyCofiber.ext_to_X Ï† i j hij hâ‚ (by simpa [snd] using hâ‚‚)

lemma ext_to_iff (i j : â„¤) (hij : i + 1 = j) {A : C} (f g : A âŸ¶ (mappingCone Ï†).X i) :
    f = g â†” f â‰« (fst Ï†).1.v i j hij = g â‰« (fst Ï†).1.v i j hij âˆ§
      f â‰« (snd Ï†).v i i (add_zero i) = g â‰« (snd Ï†).v i i (add_zero i) := by
  constructor
  Â· rintro rfl
    tauto
  Â· rintro âŸ¨hâ‚, hâ‚‚âŸ©
    exact ext_to Ï† i j hij hâ‚ hâ‚‚

lemma ext_from (i j : â„¤) (hij : j + 1 = i) {A : C} {f g : (mappingCone Ï†).X j âŸ¶ A}
    (hâ‚ : (inl Ï†).v i j (by linarith) â‰« f = (inl Ï†).v i j (by linarith) â‰« g)
    (hâ‚‚ : (inr Ï†).f j â‰« f = (inr Ï†).f j â‰« g) :
    f = g :=
  homotopyCofiber.ext_from_X Ï† i j hij hâ‚ hâ‚‚

lemma ext_from_iff (i j : â„¤) (hij : j + 1 = i) {A : C} (f g : (mappingCone Ï†).X j âŸ¶ A) :
    f = g â†” (inl Ï†).v i j (by linarith) â‰« f = (inl Ï†).v i j (by linarith) â‰« g âˆ§
      (inr Ï†).f j â‰« f = (inr Ï†).f j â‰« g := by
  constructor
  Â· rintro rfl
    tauto
  Â· rintro âŸ¨hâ‚, hâ‚‚âŸ©
    exact ext_from Ï† i j hij hâ‚ hâ‚‚

end mappingCone

end CochainComplex
