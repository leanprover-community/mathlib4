/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.HomotopyCategory.HomComplex

/-! The mapping cone

In this file, we construct the mapping cone `mappingCone Ï†` of a morphism
`Ï† : F âŸ¶ G` between cochain complexes. The definition makes sense for
cochain complexes indexed by any `Î¹ : Type*` with `[AddRightCancelSemigroup Î¹]`
and `[One Î¹]`.

In case of cochain complexes indexed by `â„¤`, the mapping cone can be studied
using the complex of homomorphisms `HomComplex`: we introduce definitions
`inl Ï† : Cochain F (mappingCone Ï†) (-1)`, `inr Ï† : G âŸ¶ mappingCone Ï†`,
`fst Ï† : Cocycle (mappingCone Ï†) F 1` and `snd Ï† : Cochain (mappingCone Ï†) G 0`.

-/

open CategoryTheory Category Limits

variable {C : Type*} [Category C] [Preadditive C]

namespace CochainComplex

section

variable {Î¹ : Type*} [AddRightCancelSemigroup Î¹] [One Î¹]
  {F G : CochainComplex C Î¹} (Ï† : F âŸ¶ G)

/-- A morphism `Ï† : F âŸ¶ G` has a mapping cone when the binary biproducts
`F.X (p + 1)) âŠž (G.X p` exist. -/
@[nolint unusedArguments]
abbrev HasMappingCone (_ : F âŸ¶ G) := âˆ€ p, HasBinaryBiproduct (F.X (p + 1)) (G.X p)

variable [HasMappingCone Ï†] [DecidableEq Î¹]

/-- The mapping cone of a morphism `Ï† : F âŸ¶ G`. In degree `i`, it consists
of `F.X (i + 1) âŠž G.X i`. -/
noncomputable def mappingCone : CochainComplex C Î¹ where
  X i := F.X (i + 1) âŠž G.X i
  d i j :=
    if h : i + 1 = j
      then -biprod.fst â‰« F.d _ _ â‰« biprod.inl +
        biprod.fst â‰« eqToHom (by rw [h]) â‰« Ï†.f j â‰« biprod.inr +
        biprod.snd â‰« G.d _ _ â‰« biprod.inr
      else 0
  shape i j (hij : i + 1 â‰  j) := dif_neg hij
  d_comp_d' := by rintro i _ _ rfl rfl; simp

namespace mappingCone

@[simp]
lemma isZero_X_iff (i : Î¹) :
    IsZero ((mappingCone Ï†).X i) â†” IsZero (F.X (i + 1)) âˆ§ IsZero (G.X i) := by
  apply biprod_isZero_iff

/-- The left injection `F.X i âŸ¶ (mappingCone Ï†).X j` when `j + 1 = i`. -/
noncomputable def inlX (i j : Î¹) (h : j + 1 = i) : F.X i âŸ¶ (mappingCone Ï†).X j :=
  eqToHom (by rw [h]) â‰« biprod.inl

/-- The right injection `G.X i âŸ¶ (mappingCone Ï†).X i`. -/
noncomputable def inrX (i : Î¹) : G.X i âŸ¶ (mappingCone Ï†).X i := biprod.inr

/-- The first projection `(mappingCone Ï†).X i âŸ¶ F.X j` when `i + 1 = j`. -/
noncomputable def fstX (i j : Î¹) (h : i + 1 = j) : (mappingCone Ï†).X i âŸ¶ F.X j :=
  biprod.fst â‰« eqToHom (by rw [h])

/-- The second projection `(mappingCone Ï†).X i âŸ¶ G.X i`. -/
noncomputable def sndX (i : Î¹) : (mappingCone Ï†).X i âŸ¶ G.X i := biprod.snd

@[reassoc (attr := simp)]
lemma inlX_fstX (i j : Î¹) (h : j + 1 = i) :
    inlX Ï† i j h â‰« fstX Ï† j i h = ðŸ™ _ := by
  subst h
  simp [inlX, fstX]

@[reassoc (attr := simp)]
lemma inlX_sndX (i j : Î¹) (h : j + 1 = i) :
    inlX Ï† i j h â‰« sndX Ï† j = 0 := by
  subst h
  simp [inlX, sndX]

@[reassoc (attr := simp)]
lemma inrX_fstX (i j : Î¹) (h : i + 1 = j) :
    inrX Ï† i â‰« fstX Ï† i j h = 0 := by
  subst h
  simp [inrX, fstX]

@[reassoc (attr := simp)]
lemma inrX_sndX (i : Î¹) :
    inrX Ï† i â‰« sndX Ï† i = ðŸ™ _ := by
  simp [inrX, sndX]

@[reassoc]
lemma inlX_d (i j k : Î¹) (h : j + 1 = i) (h' : i + 1 = k) :
    inlX Ï† i j h â‰« (mappingCone Ï†).d j i = -F.d i k â‰« inlX Ï† k i h' + Ï†.f i â‰« inrX Ï† i := by
  subst h h'
  simp [inlX, inrX, mappingCone]

@[reassoc]
lemma inrX_d (i j : Î¹) (h : i + 1 = j) :
    inrX Ï† i â‰« (mappingCone Ï†).d i j = G.d i j â‰« inrX Ï† j := by
  subst h
  simp [inrX, mappingCone]

lemma id_X (i j : Î¹) (h : i + 1 = j) :
    ðŸ™ ((mappingCone Ï†).X i) = fstX Ï† i j h â‰« inlX Ï† j i h + sndX Ï† i â‰« inrX Ï† i := by
  subst h
  simp only [fstX, eqToHom_refl, comp_id, inlX, id_comp, sndX, inrX]
  symm
  apply biprod.total

lemma extX (i j : Î¹) (h : i + 1 = j) {A : C} {f g : A âŸ¶ (mappingCone Ï†).X i}
    (hâ‚ : f â‰« fstX Ï† i j h = g â‰« fstX Ï† i j h) (hâ‚‚ : f â‰« sndX Ï† i = g â‰« sndX Ï† i) :
    f = g := by
  subst h
  apply biprod.hom_ext
  Â· simpa [fstX] using hâ‚
  Â· simpa using hâ‚‚

lemma extX' (i j : Î¹) (h : j + 1 = i) {A : C} {f g : (mappingCone Ï†).X j âŸ¶ A}
    (hâ‚ : inlX Ï† i j h â‰« f = inlX Ï† i j h â‰« g) (hâ‚‚ : inrX Ï† j â‰« f = inrX Ï† j â‰« g) :
    f = g := by
  subst h
  apply biprod.hom_ext'
  Â· simpa [inlX] using hâ‚
  Â· simpa using hâ‚‚

attribute [irreducible] mappingCone inlX inrX fstX sndX

/-- The bilimit binary bicone expressing that `(mappingCone Ï†).X i` identifies to the binary
biproduct of `F.X j` and `G.X i` when `i + 1 = j`. -/
@[simps]
noncomputable def binaryBicone (i j : Î¹) (h : i + 1 = j) : BinaryBicone (F.X j) (G.X i) where
  pt := (mappingCone Ï†).X i
  fst := fstX Ï† i j h
  snd := sndX Ï† i
  inl := inlX Ï† j i h
  inr := inrX Ï† i

/-- `(mappingCone Ï†).X i` identifies to the binary biproduct of
`F.X j` and `G.X i` when `i + 1 = j`. -/
noncomputable def binaryBiconeIsBilimit (i j : Î¹) (h : i + 1 = j) :
    (binaryBicone Ï† i j h).IsBilimit :=
  isBinaryBilimitOfTotal _ (by simp [id_X Ï† i j h])

/-- The right injection `G âŸ¶ mappingCone Ï†`. -/
@[simps]
noncomputable def inr : G âŸ¶ mappingCone Ï† where
  f i := inrX Ï† i
  comm' i j hij := inrX_d Ï† i j hij

end mappingCone

end

variable {F G : CochainComplex C â„¤} (Ï† : F âŸ¶ G) [HasMappingCone Ï†]

open HomComplex

namespace mappingCone

/-- The left injection, as a cochain in `Cochain F (mappingCone Ï†) (-1)`. -/
noncomputable def inl : Cochain F (mappingCone Ï†) (-1) :=
  Cochain.mk (fun p q hpq => inlX Ï† p q  (by linarith))

@[simp]
lemma inl_v (p q : â„¤) (hpq : p + (-1) = q) :
    (inl Ï†).v p q hpq = inlX Ï† p q (by linarith) := rfl

/-- The first projection, as a cocycle in `Cocycle (mappingCone Ï†) F 1`. -/
noncomputable def fst : Cocycle (mappingCone Ï†) F 1 :=
  Cocycle.mk (Cochain.mk (fstX Ï†)) 2 (by linarith) (by
    ext p _ rfl
    rw [Î´_v 1 2 (by linarith) _ p (p + 2) rfl (p + 1) (p + 1) (by linarith) rfl]
    apply extX' _ _ _ rfl
    Â· simp [inlX_d_assoc Ï† (p + 1) p (p + 2) rfl (by linarith),
        show Int.negOnePow 2 = 1 from rfl]
    Â· simp [inrX_d_assoc Ï† p (p + 1) rfl])

@[simp]
lemma fst_v (p q : â„¤) (hpq : p + 1 = q) :
    (fst Ï†).1.v p q hpq = fstX Ï† p q hpq := rfl

/-- The second projection, as a cochain in `Cochain (mappingCone Ï†) G 0`. -/
noncomputable def snd : Cochain (mappingCone Ï†) G 0 :=
  Cochain.ofHoms (sndX Ï†)

lemma snd_v (i : â„¤) : (snd Ï†).v i i (add_zero i) = sndX Ï† i := by
  simp only [snd, Cochain.ofHoms_v]

end mappingCone

end CochainComplex
