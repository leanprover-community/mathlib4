/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.HomotopyCategory.HomComplex
import Mathlib.Algebra.Homology.HomotopyCofiber

/-! # The mapping cone of a morphism of cochain complexes

In this file, we study the homotopy cofiber `HomologicalComplex.homotopyCofiber`
of a morphism `Ï† : F âŸ¶ G` of cochain complexes indexed by `â„¤`. In this case,
we redefine it as `CochainComplex.mappingCone Ï†`. The API involves definitions
- `mappingCone.inl Ï† : Cochain F (mappingCone Ï†) (-1)`,
- `mappingCone.inr Ï† : G âŸ¶ mappingCone Ï†`,
- `mappingCone.fst Ï† : Cocycle (mappingCone Ï†) F 1` and
- `mappingCone.snd Ï† : Cochain (mappingCone Ï†) G 0`.

-/

open CategoryTheory Limits

variable {C : Type*} [Category C] [Preadditive C]

namespace CochainComplex

open HomologicalComplex

section

variable {Î¹ : Type*} [AddRightCancelSemigroup Î¹] [One Î¹]
    {F G : CochainComplex C Î¹} (Ï† : F âŸ¶ G)

instance [âˆ€ p, HasBinaryBiproduct (F.X (p + 1)) (G.X p)] :
    HasHomotopyCofiber Ï† where
  hasBinaryBiproduct := by
    rintro i _ rfl
    infer_instance

end

variable {F G : CochainComplex C â„¤} (Ï† : F âŸ¶ G)
variable [HasHomotopyCofiber Ï†]

/-- The mapping cone of a morphism of cochain complexes indexed by `â„¤`. -/
noncomputable def mappingCone := homotopyCofiber Ï†

namespace mappingCone

open HomComplex

/-- The left inclusion in the mapping cone, as a cochain of degree `-1`. -/
noncomputable def inl : Cochain F (mappingCone Ï†) (-1) :=
  Cochain.mk (fun p q hpq => homotopyCofiber.inlX Ï† p q  (by dsimp; omega))

/-- The right inclusion in the mapping cone. -/
noncomputable def inr : G âŸ¶ mappingCone Ï† := homotopyCofiber.inr Ï†

/-- The first projection from the mapping cone, as a cocyle of degree `1`. -/
noncomputable def fst : Cocycle (mappingCone Ï†) F 1 :=
  Cocycle.mk (Cochain.mk (fun p q hpq => homotopyCofiber.fstX Ï† p q hpq)) 2 (by omega) (by
    ext p _ rfl
    simp [Î´_v 1 2 (by omega) _ p (p + 2) (by omega) (p + 1) (p + 1) (by omega) rfl,
      homotopyCofiber.d_fstX Ï† p (p + 1) (p + 2) rfl, mappingCone,
      show Int.negOnePow 2 = 1 by rfl])

/-- The second projection from the mapping cone, as a cochain of degree `0`. -/
noncomputable def snd : Cochain (mappingCone Ï†) G 0 :=
  Cochain.ofHoms (homotopyCofiber.sndX Ï†)

@[reassoc (attr := simp)]
lemma inl_v_fst_v (p q : â„¤) (hpq : q + 1 = p) :
    (inl Ï†).v p q (by rw [â† hpq, add_neg_cancel_right]) â‰«
      (fst Ï† : Cochain (mappingCone Ï†) F 1).v q p hpq = ğŸ™ _ := by
  simp [inl, fst]

@[reassoc (attr := simp)]
lemma inl_v_snd_v (p q : â„¤) (hpq : p + (-1) = q) :
    (inl Ï†).v p q hpq â‰« (snd Ï†).v q q (add_zero q) = 0 := by
  simp [inl, snd]

@[reassoc (attr := simp)]
lemma inr_f_fst_v (p q : â„¤) (hpq : p + 1 = q) :
    (inr Ï†).f p â‰« (fst Ï†).1.v p q hpq = 0 := by
  simp [inr, fst]

@[reassoc (attr := simp)]
lemma inr_f_snd_v (p : â„¤) :
    (inr Ï†).f p â‰« (snd Ï†).v p p (add_zero p) = ğŸ™ _ := by
  simp [inr, snd]

@[simp]
lemma inl_fst :
    (inl Ï†).comp (fst Ï†).1 (neg_add_self 1) = Cochain.ofHom (ğŸ™ F) := by
  ext p
  simp [Cochain.comp_v _ _ (neg_add_self 1) p (p-1) p rfl (by omega)]

@[simp]
lemma inl_snd :
    (inl Ï†).comp (snd Ï†) (add_zero (-1)) = 0 := by
  ext p q hpq
  simp [Cochain.comp_v _ _ (add_zero (-1)) p q q (by omega) (by omega)]

@[simp]
lemma inr_fst :
    (Cochain.ofHom (inr Ï†)).comp (fst Ï†).1 (zero_add 1) = 0 := by
  ext p q hpq
  simp [Cochain.comp_v _ _ (zero_add 1) p p q (by omega) (by omega)]

@[simp]
lemma inr_snd :
    (Cochain.ofHom (inr Ï†)).comp (snd Ï†) (zero_add 0) = Cochain.ofHom (ğŸ™ G) := by aesop_cat

/-! In order to obtain identities of cochains involving `inl`, `inr`, `fst` and `snd`,
it is often convenient to use an `ext` lemma, and use simp lemmas like `inl_v_f_fst_v`,
but it is sometimes possible to get identities of cochains by using rewrites of
identities of cochains like `inl_fst`. Then, similarly as in category theory,
if we associate the compositions of cochains to the right as much as possible,
it is also interesting to have `reassoc` variants of lemmas, like `inl_fst_assoc`. -/

@[simp]
lemma inl_fst_assoc {K : CochainComplex C â„¤} {d e : â„¤} (Î³ : Cochain F K d) (he : 1 + d = e) :
    (inl Ï†).comp ((fst Ï†).1.comp Î³ he) (by rw [â† he, neg_add_cancel_left]) = Î³ := by
  rw [â† Cochain.comp_assoc _ _ _ (neg_add_self 1) (by omega) (by omega), inl_fst,
    Cochain.id_comp]

@[simp]
lemma inl_snd_assoc {K : CochainComplex C â„¤} {d e f : â„¤} (Î³ : Cochain G K d)
    (he : 0 + d = e) (hf : -1 + e = f) :
    (inl Ï†).comp ((snd Ï†).comp Î³ he) hf = 0 := by
  obtain rfl : e = d := by omega
  rw [â† Cochain.comp_assoc_of_second_is_zero_cochain, inl_snd, Cochain.zero_comp]

@[simp]
lemma inr_fst_assoc {K : CochainComplex C â„¤} {d e f : â„¤} (Î³ : Cochain F K d)
    (he : 1 + d = e) (hf : 0 + e = f) :
    (Cochain.ofHom (inr Ï†)).comp ((fst Ï†).1.comp Î³ he) hf = 0 := by
  obtain rfl : e = f := by omega
  rw [â† Cochain.comp_assoc_of_first_is_zero_cochain, inr_fst, Cochain.zero_comp]

@[simp]
lemma inr_snd_assoc {K : CochainComplex C â„¤} {d e : â„¤} (Î³ : Cochain G K d) (he : 0 + d = e) :
    (Cochain.ofHom (inr Ï†)).comp ((snd Ï†).comp Î³ he) (by simp only [â† he, zero_add]) = Î³ := by
  obtain rfl : d = e := by omega
  rw [â† Cochain.comp_assoc_of_first_is_zero_cochain, inr_snd, Cochain.id_comp]

lemma ext_to (i j : â„¤) (hij : i + 1 = j) {A : C} {f g : A âŸ¶ (mappingCone Ï†).X i}
    (hâ‚ : f â‰« (fst Ï†).1.v i j hij = g â‰« (fst Ï†).1.v i j hij)
    (hâ‚‚ : f â‰« (snd Ï†).v i i (add_zero i) = g â‰« (snd Ï†).v i i (add_zero i)) :
    f = g :=
  homotopyCofiber.ext_to_X Ï† i j hij hâ‚ (by simpa [snd] using hâ‚‚)

lemma ext_to_iff (i j : â„¤) (hij : i + 1 = j) {A : C} (f g : A âŸ¶ (mappingCone Ï†).X i) :
    f = g â†” f â‰« (fst Ï†).1.v i j hij = g â‰« (fst Ï†).1.v i j hij âˆ§
      f â‰« (snd Ï†).v i i (add_zero i) = g â‰« (snd Ï†).v i i (add_zero i) := by
  constructor
  Â· rintro rfl
    tauto
  Â· rintro âŸ¨hâ‚, hâ‚‚âŸ©
    exact ext_to Ï† i j hij hâ‚ hâ‚‚

lemma ext_from (i j : â„¤) (hij : j + 1 = i) {A : C} {f g : (mappingCone Ï†).X j âŸ¶ A}
    (hâ‚ : (inl Ï†).v i j (by omega) â‰« f = (inl Ï†).v i j (by omega) â‰« g)
    (hâ‚‚ : (inr Ï†).f j â‰« f = (inr Ï†).f j â‰« g) :
    f = g :=
  homotopyCofiber.ext_from_X Ï† i j hij hâ‚ hâ‚‚

lemma ext_from_iff (i j : â„¤) (hij : j + 1 = i) {A : C} (f g : (mappingCone Ï†).X j âŸ¶ A) :
    f = g â†” (inl Ï†).v i j (by omega) â‰« f = (inl Ï†).v i j (by omega) â‰« g âˆ§
      (inr Ï†).f j â‰« f = (inr Ï†).f j â‰« g := by
  constructor
  Â· rintro rfl
    tauto
  Â· rintro âŸ¨hâ‚, hâ‚‚âŸ©
    exact ext_from Ï† i j hij hâ‚ hâ‚‚

lemma ext_cochain_to_iff (i j : â„¤) (hij : i + 1 = j)
    {K : CochainComplex C â„¤} {Î³â‚ Î³â‚‚ : Cochain K (mappingCone Ï†) i} :
    Î³â‚ = Î³â‚‚ â†” Î³â‚.comp (fst Ï†).1 hij = Î³â‚‚.comp (fst Ï†).1 hij âˆ§
      Î³â‚.comp (snd Ï†) (add_zero i) = Î³â‚‚.comp (snd Ï†) (add_zero i) := by
  constructor
  Â· rintro rfl
    tauto
  Â· rintro âŸ¨hâ‚, hâ‚‚âŸ©
    ext p q hpq
    rw [ext_to_iff Ï† q (q + 1) rfl]
    replace hâ‚ := Cochain.congr_v hâ‚ p (q + 1) (by omega)
    replace hâ‚‚ := Cochain.congr_v hâ‚‚ p q hpq
    simp only [Cochain.comp_v _ _ _ p q (q + 1) hpq rfl] at hâ‚
    simp only [Cochain.comp_zero_cochain_v] at hâ‚‚
    exact âŸ¨hâ‚, hâ‚‚âŸ©

lemma ext_cochain_from_iff (i j : â„¤) (hij : i + 1 = j)
    {K : CochainComplex C â„¤} {Î³â‚ Î³â‚‚ : Cochain (mappingCone Ï†) K j} :
    Î³â‚ = Î³â‚‚ â†”
      (inl Ï†).comp Î³â‚ (show _ = i by omega) = (inl Ï†).comp Î³â‚‚ (by omega) âˆ§
        (Cochain.ofHom (inr Ï†)).comp Î³â‚ (zero_add j) =
          (Cochain.ofHom (inr Ï†)).comp Î³â‚‚ (zero_add j) := by
  constructor
  Â· rintro rfl
    tauto
  Â· rintro âŸ¨hâ‚, hâ‚‚âŸ©
    ext p q hpq
    rw [ext_from_iff Ï† (p + 1) p rfl]
    replace hâ‚ := Cochain.congr_v hâ‚ (p + 1) q (by omega)
    replace hâ‚‚ := Cochain.congr_v hâ‚‚ p q (by omega)
    simp only [Cochain.comp_v (inl Ï†) _ _ (p + 1) p q (by omega) hpq] at hâ‚
    simp only [Cochain.zero_cochain_comp_v, Cochain.ofHom_v] at hâ‚‚
    exact âŸ¨hâ‚, hâ‚‚âŸ©

lemma id :
    (fst Ï†).1.comp (inl Ï†) (add_neg_self 1) +
      (snd Ï†).comp (Cochain.ofHom (inr Ï†)) (add_zero 0) = Cochain.ofHom (ğŸ™ _) := by
  simp [ext_cochain_from_iff Ï† (-1) 0 (neg_add_self 1)]

lemma id_X (p q : â„¤) (hpq : p + 1 = q) :
    (fst Ï†).1.v p q hpq â‰« (inl Ï†).v q p (by omega) +
      (snd Ï†).v p p (add_zero p) â‰« (inr Ï†).f p = ğŸ™ ((mappingCone Ï†).X p) := by
  simpa only [Cochain.add_v, Cochain.comp_zero_cochain_v, Cochain.ofHom_v, id_f,
    Cochain.comp_v _ _ (add_neg_self 1) p q p hpq (by omega)]
    using Cochain.congr_v (id Ï†) p p (add_zero p)

@[reassoc]
lemma inl_v_d (i j k : â„¤) (hij : i + (-1) = j) (hik : k + (-1) = i) :
    (inl Ï†).v i j hij â‰« (mappingCone Ï†).d j i =
      Ï†.f i â‰« (inr Ï†).f i - F.d i k â‰« (inl Ï†).v _ _ hik := by
  dsimp [mappingCone, inl, inr]
  rw [homotopyCofiber.inlX_d Ï† j i k (by dsimp; omega) (by dsimp; omega)]
  abel

@[reassoc (attr := simp 1100)]
lemma inr_f_d (nâ‚ nâ‚‚ : â„¤) :
    (inr Ï†).f nâ‚ â‰« (mappingCone Ï†).d nâ‚ nâ‚‚ = G.d nâ‚ nâ‚‚ â‰« (inr Ï†).f nâ‚‚ := by
  apply Hom.comm

@[reassoc]
lemma d_fst_v (i j k : â„¤) (hij : i + 1 = j) (hjk : j + 1 = k) :
    (mappingCone Ï†).d i j â‰« (fst Ï†).1.v j k hjk =
      -(fst Ï†).1.v i j hij â‰« F.d j k := by
  apply homotopyCofiber.d_fstX

@[reassoc (attr := simp)]
lemma d_fst_v' (i j : â„¤) (hij : i + 1 = j) :
    (mappingCone Ï†).d (i - 1) i â‰« (fst Ï†).1.v i j hij =
      -(fst Ï†).1.v (i - 1) i (by omega) â‰« F.d i j :=
  d_fst_v Ï† (i - 1) i j (by omega) hij

@[reassoc]
lemma d_snd_v (i j : â„¤) (hij : i + 1 = j) :
    (mappingCone Ï†).d i j â‰« (snd Ï†).v j j (add_zero _) =
      (fst Ï†).1.v i j hij â‰« Ï†.f j + (snd Ï†).v i i (add_zero i) â‰« G.d i j := by
  dsimp [mappingCone, snd, fst]
  simp only [Cochain.ofHoms_v]
  apply homotopyCofiber.d_sndX

@[reassoc (attr := simp)]
lemma d_snd_v' (n : â„¤) :
    (mappingCone Ï†).d (n - 1) n â‰« (snd Ï†).v n n (add_zero n) =
    (fst Ï† : Cochain (mappingCone Ï†) F 1).v (n - 1) n (by omega) â‰« Ï†.f n +
      (snd Ï†).v (n - 1) (n - 1) (add_zero _) â‰« G.d (n - 1) n := by
  apply d_snd_v

@[simp]
lemma Î´_inl :
    Î´ (-1) 0 (inl Ï†) = Cochain.ofHom (Ï† â‰« inr Ï†) := by
  ext p
  simp [Î´_v (-1) 0 (neg_add_self 1) (inl Ï†) p p (add_zero p) _ _ rfl rfl,
    inl_v_d Ï† p (p - 1) (p + 1) (by omega) (by omega)]

@[simp]
lemma Î´_snd :
    Î´ 0 1 (snd Ï†) = -(fst Ï†).1.comp (Cochain.ofHom Ï†) (add_zero 1) := by
  ext p q hpq
  simp [d_snd_v Ï† p q hpq]

section

variable {K : CochainComplex C â„¤} {n m : â„¤} (Î± : Cochain F K m)
    (Î² : Cochain G K n) (h : m + 1 = n)

/-- Given `Ï† : F âŸ¶ G`, this is the cochain in `Cochain (mappingCone Ï†) K n` that is
constructed from two cochains `Î± : Cochain F K m` (with `m + 1 = n`) and `Î² : Cochain F K n`. -/
noncomputable def descCochain : Cochain (mappingCone Ï†) K n :=
  (fst Ï†).1.comp Î± (by rw [â† h, add_comm]) + (snd Ï†).comp Î² (zero_add n)

@[simp]
lemma inl_descCochain :
    (inl Ï†).comp (descCochain Ï† Î± Î² h) (by omega) = Î± := by
  simp [descCochain]

@[simp]
lemma inr_descCochain :
    (Cochain.ofHom (inr Ï†)).comp (descCochain Ï† Î± Î² h) (zero_add n) = Î² := by
  simp [descCochain]

@[reassoc (attr := simp)]
lemma inl_v_descCochain_v (pâ‚ pâ‚‚ pâ‚ƒ : â„¤) (hâ‚â‚‚ : pâ‚ + (-1) = pâ‚‚) (hâ‚‚â‚ƒ : pâ‚‚ + n = pâ‚ƒ) :
    (inl Ï†).v pâ‚ pâ‚‚ hâ‚â‚‚ â‰« (descCochain Ï† Î± Î² h).v pâ‚‚ pâ‚ƒ hâ‚‚â‚ƒ =
        Î±.v pâ‚ pâ‚ƒ (by rw [â† hâ‚‚â‚ƒ, â† hâ‚â‚‚, â† h, add_comm m, add_assoc, neg_add_cancel_left]) := by
  simpa only [Cochain.comp_v _ _ (show -1 + n = m by omega) pâ‚ pâ‚‚ pâ‚ƒ
    (by omega) (by omega)] using
      Cochain.congr_v (inl_descCochain Ï† Î± Î² h) pâ‚ pâ‚ƒ (by omega)

@[reassoc (attr := simp)]
lemma inr_f_descCochain_v (pâ‚ pâ‚‚ : â„¤) (hâ‚â‚‚ : pâ‚ + n = pâ‚‚) :
    (inr Ï†).f pâ‚ â‰« (descCochain Ï† Î± Î² h).v pâ‚ pâ‚‚ hâ‚â‚‚ = Î².v pâ‚ pâ‚‚ hâ‚â‚‚ := by
  simpa only [Cochain.comp_v _ _ (zero_add n) pâ‚ pâ‚ pâ‚‚ (add_zero pâ‚) hâ‚â‚‚, Cochain.ofHom_v]
    using Cochain.congr_v (inr_descCochain Ï† Î± Î² h) pâ‚ pâ‚‚ (by omega)

lemma Î´_descCochain (n' : â„¤) (hn' : n + 1 = n') :
    Î´ n n' (descCochain Ï† Î± Î² h) =
      (fst Ï†).1.comp (Î´ m n Î± +
          n'.negOnePow â€¢ (Cochain.ofHom Ï†).comp Î² (zero_add n)) (by omega) +
      (snd Ï†).comp (Î´ n n' Î²) (zero_add n') := by
  dsimp only [descCochain]
  simp only [Î´_add, Cochain.comp_add, Î´_comp (fst Ï†).1 Î± _ 2 n n' hn' (by omega) (by omega),
    Cocycle.Î´_eq_zero, Cochain.zero_comp, smul_zero, add_zero,
    Î´_comp (snd Ï†) Î² (zero_add n) 1 n' n' hn' (zero_add 1) hn', Î´_snd, Cochain.neg_comp,
    smul_neg, Cochain.comp_assoc_of_second_is_zero_cochain, Cochain.comp_units_smul, â† hn',
    Int.negOnePow_succ, Units.neg_smul, Cochain.comp_neg]
  abel

end

/-- Given `Ï† : F âŸ¶ G`, this is the cocycle in `Cocycle (mappingCone Ï†) K n` that is
constructed from `Î± : Cochain F K m` (with `m + 1 = n`) and `Î² : Cocycle F K n`,
when a suitable cocycle relation is satisfied. -/
@[simps!]
noncomputable def descCocycle {K : CochainComplex C â„¤} {n m : â„¤}
    (Î± : Cochain F K m) (Î² : Cocycle G K n)
    (h : m + 1 = n) (eq : Î´ m n Î± = n.negOnePow â€¢ (Cochain.ofHom Ï†).comp Î².1 (zero_add n)) :
    Cocycle (mappingCone Ï†) K n :=
  Cocycle.mk (descCochain Ï† Î± Î².1 h) (n + 1) rfl
    (by simp [Î´_descCochain _ _ _ _ _ rfl, eq, Int.negOnePow_succ])

section

variable {K : CochainComplex C â„¤} (Î± : Cochain F K (-1)) (Î² : G âŸ¶ K)
  (eq : Î´ (-1) 0 Î± = Cochain.ofHom (Ï† â‰« Î²))

/-- Given `Ï† : F âŸ¶ G`, this is the morphism `mappingCone Ï† âŸ¶ K` that is constructed
from a cochain `Î± : Cochain F K (-1)` and a morphism `Î² : G âŸ¶ K` such that
`Î´ (-1) 0 Î± = Cochain.ofHom (Ï† â‰« Î²)`. -/
noncomputable def desc : mappingCone Ï† âŸ¶ K :=
  Cocycle.homOf (descCocycle Ï† Î± (Cocycle.ofHom Î²) (neg_add_self 1) (by simp [eq]))

@[simp]
lemma ofHom_desc :
    Cochain.ofHom (desc Ï† Î± Î² eq) = descCochain Ï† Î± (Cochain.ofHom Î²) (neg_add_self 1) := by
  simp [desc]

@[reassoc (attr := simp)]
lemma inl_v_desc_f (p q : â„¤) (h : p + (-1) = q) :
    (inl Ï†).v p q h â‰« (desc Ï† Î± Î² eq).f q = Î±.v p q h := by
  simp [desc]

lemma inl_desc :
    (inl Ï†).comp (Cochain.ofHom (desc Ï† Î± Î² eq)) (add_zero _) = Î± := by
  simp

@[reassoc (attr := simp)]
lemma inr_f_desc_f (p : â„¤) :
    (inr Ï†).f p â‰« (desc Ï† Î± Î² eq).f p = Î².f p := by
  simp [desc]

@[reassoc (attr := simp)]
lemma inr_desc : inr Ï† â‰« desc Ï† Î± Î² eq = Î² := by aesop_cat

lemma desc_f (p q : â„¤) (hpq : p + 1 = q) :
    (desc Ï† Î± Î² eq).f p = (fst Ï†).1.v p q hpq â‰« Î±.v q p (by omega) +
      (snd Ï†).v p p (add_zero p) â‰« Î².f p := by
  simp [ext_from_iff _ _ _ hpq]

end

/-- Constructor for homotopies between morphisms from a mapping cone. -/
noncomputable def descHomotopy {K : CochainComplex C â„¤} (fâ‚ fâ‚‚ : mappingCone Ï† âŸ¶ K)
    (Î³â‚ : Cochain F K (-2)) (Î³â‚‚ : Cochain G K (-1))
    (hâ‚ : (inl Ï†).comp (Cochain.ofHom fâ‚) (add_zero (-1))  =
      Î´ (-2) (-1) Î³â‚ + (Cochain.ofHom Ï†).comp Î³â‚‚ (zero_add (-1)) +
      (inl Ï†).comp (Cochain.ofHom fâ‚‚) (add_zero (-1)))
    (hâ‚‚ : Cochain.ofHom (inr Ï† â‰« fâ‚) = Î´ (-1) 0 Î³â‚‚ + Cochain.ofHom (inr Ï† â‰« fâ‚‚)) :
    Homotopy fâ‚ fâ‚‚ :=
  (Cochain.equivHomotopy fâ‚ fâ‚‚).symm âŸ¨descCochain Ï† Î³â‚ Î³â‚‚ (by norm_num), by
    simp only [Cochain.ofHom_comp] at hâ‚‚
    simp [ext_cochain_from_iff _ _ _ (neg_add_self 1),
      Î´_descCochain _ _ _ _ _ (neg_add_self 1), hâ‚, hâ‚‚]âŸ©

section

variable {K : CochainComplex C â„¤} {n m : â„¤}
    (Î± : Cochain K F m) (Î² : Cochain K G n) (h : n + 1 = m)

/-- Given `Ï† : F âŸ¶ G`, this is the cochain in `Cochain (mappingCone Ï†) K n` that is
constructed from two cochains `Î± : Cochain F K m` (with `m + 1 = n`) and `Î² : Cochain F K n`. -/
noncomputable def liftCochain : Cochain K (mappingCone Ï†) n :=
  Î±.comp (inl Ï†) (by omega) + Î².comp (Cochain.ofHom (inr Ï†)) (add_zero n)

@[simp]
lemma liftCochain_fst :
    (liftCochain Ï† Î± Î² h).comp (fst Ï†).1 h = Î± := by
  simp [liftCochain]

@[simp]
lemma liftCochain_snd :
    (liftCochain Ï† Î± Î² h).comp (snd Ï†) (add_zero n) = Î² := by
  simp [liftCochain]

@[reassoc (attr := simp)]
lemma liftCochain_v_fst_v (pâ‚ pâ‚‚ pâ‚ƒ : â„¤) (hâ‚â‚‚ : pâ‚ + n = pâ‚‚) (hâ‚‚â‚ƒ : pâ‚‚ + 1 = pâ‚ƒ) :
    (liftCochain Ï† Î± Î² h).v pâ‚ pâ‚‚ hâ‚â‚‚ â‰« (fst Ï†).1.v pâ‚‚ pâ‚ƒ hâ‚‚â‚ƒ = Î±.v pâ‚ pâ‚ƒ (by omega) := by
  simpa only [Cochain.comp_v _ _ h pâ‚ pâ‚‚ pâ‚ƒ hâ‚â‚‚ hâ‚‚â‚ƒ]
    using Cochain.congr_v (liftCochain_fst Ï† Î± Î² h) pâ‚ pâ‚ƒ (by omega)

@[reassoc (attr := simp)]
lemma liftCochain_v_snd_v (pâ‚ pâ‚‚ : â„¤) (hâ‚â‚‚ : pâ‚ + n = pâ‚‚) :
    (liftCochain Ï† Î± Î² h).v pâ‚ pâ‚‚ hâ‚â‚‚ â‰« (snd Ï†).v pâ‚‚ pâ‚‚ (add_zero pâ‚‚) = Î².v pâ‚ pâ‚‚ hâ‚â‚‚ := by
  simpa only [Cochain.comp_v _ _ (add_zero n) pâ‚ pâ‚‚ pâ‚‚ hâ‚â‚‚ (add_zero pâ‚‚)]
    using Cochain.congr_v (liftCochain_snd Ï† Î± Î² h) pâ‚ pâ‚‚ (by omega)

lemma Î´_liftCochain (m' : â„¤) (hm' : m + 1 = m') :
    Î´ n m (liftCochain Ï† Î± Î² h) = -(Î´ m m' Î±).comp (inl Ï†) (by omega) +
      (Î´ n m Î² + Î±.comp (Cochain.ofHom Ï†) (add_zero m)).comp
        (Cochain.ofHom (inr Ï†)) (add_zero m) := by
  dsimp only [liftCochain]
  simp only [Î´_add, Î´_comp Î± (inl Ï†) _ m' _ _ h hm' (neg_add_self 1),
    Î´_comp_zero_cochain _ _ _ h, Î´_inl, Cochain.ofHom_comp,
    Int.negOnePow_neg, Int.negOnePow_one, Units.neg_smul, one_smul,
    Î´_ofHom, Cochain.comp_zero, zero_add, Cochain.add_comp,
    Cochain.comp_assoc_of_second_is_zero_cochain]
  abel

end

/-- Given `Ï† : F âŸ¶ G`, this is the cocycle in `Cocycle K (mappingCone Ï†) n` that is
constructed from `Î± : Cochain K F m` (with `n + 1 = m`) and `Î² : Cocycle K G n`,
when a suitable cocycle relation is satisfied. -/
@[simps!]
noncomputable def liftCocycle {K : CochainComplex C â„¤} {n m : â„¤}
    (Î± : Cocycle K F m) (Î² : Cochain K G n) (h : n + 1 = m)
    (eq : Î´ n m Î² + Î±.1.comp (Cochain.ofHom Ï†) (add_zero m) = 0) :
    Cocycle K (mappingCone Ï†) n :=
  Cocycle.mk (liftCochain Ï† Î± Î² h) m h (by
    simp only [Î´_liftCochain Ï† Î± Î² h (m+1) rfl, eq,
      Cocycle.Î´_eq_zero, Cochain.zero_comp, neg_zero, add_zero])

section

variable {K : CochainComplex C â„¤} (Î± : Cocycle K F 1) (Î² : Cochain K G 0)
    (eq : Î´ 0 1 Î² + Î±.1.comp (Cochain.ofHom Ï†) (add_zero 1) = 0)

/-- Given `Ï† : F âŸ¶ G`, this is the morphism `K âŸ¶ mappingCone Ï†` that is constructed
from a cocycle `Î± : Cochain K F 1` and a cochain `Î² : Cochain K G 0`
when a suitable cocycle relation is satisfied. -/
noncomputable def lift :
    K âŸ¶ mappingCone Ï† :=
  Cocycle.homOf (liftCocycle Ï† Î± Î² (zero_add 1) eq)

@[simp]
lemma ofHom_lift :
    Cochain.ofHom (lift Ï† Î± Î² eq) = liftCochain Ï† Î± Î² (zero_add 1) := by
  simp only [lift, Cocycle.cochain_ofHom_homOf_eq_coe, liftCocycle_coe]

@[reassoc (attr := simp)]
lemma lift_f_fst_v (p q : â„¤) (hpq : p + 1 = q) :
    (lift Ï† Î± Î² eq).f p â‰« (fst Ï†).1.v p q hpq = Î±.1.v p q hpq := by
  simp [lift]

lemma lift_fst :
    (Cochain.ofHom (lift Ï† Î± Î² eq)).comp (fst Ï†).1 (zero_add 1) = Î±.1 := by simp

@[reassoc (attr := simp)]
lemma lift_f_snd_v (p q : â„¤) (hpq : p + 0 = q) :
    (lift Ï† Î± Î² eq).f p â‰« (snd Ï†).v p q hpq = Î².v p q hpq := by
  obtain rfl : q = p := by omega
  simp [lift]

lemma lift_snd :
    (Cochain.ofHom (lift Ï† Î± Î² eq)).comp (snd Ï†) (zero_add 0) = Î² := by simp

lemma lift_f (p q : â„¤) (hpq : p + 1 = q) :
    (lift Ï† Î± Î² eq).f p = Î±.1.v p q hpq â‰«
      (inl Ï†).v q p (by omega) + Î².v p p (add_zero p) â‰« (inr Ï†).f p := by
  simp [ext_to_iff _ _ _ hpq]

end

/-- Constructor for homotopies between morphisms to a mapping cone. -/
noncomputable def liftHomotopy {K : CochainComplex C â„¤} (fâ‚ fâ‚‚ : K âŸ¶ mappingCone Ï†)
    (Î± : Cochain K F 0) (Î² : Cochain K G (-1))
    (hâ‚ : (Cochain.ofHom fâ‚).comp (fst Ï†).1 (zero_add 1) =
      -Î´ 0 1 Î± + (Cochain.ofHom fâ‚‚).comp (fst Ï†).1 (zero_add 1))
    (hâ‚‚ : (Cochain.ofHom fâ‚).comp (snd Ï†) (zero_add 0) =
      Î´ (-1) 0 Î² + Î±.comp (Cochain.ofHom Ï†) (zero_add 0) +
        (Cochain.ofHom fâ‚‚).comp (snd Ï†) (zero_add 0)) :
    Homotopy fâ‚ fâ‚‚ :=
  (Cochain.equivHomotopy fâ‚ fâ‚‚).symm âŸ¨liftCochain Ï† Î± Î² (neg_add_self 1), by
    simp [Î´_liftCochain _ _ _ _ _ (zero_add 1), ext_cochain_to_iff _ _ _ (zero_add 1), hâ‚, hâ‚‚]âŸ©

section

variable {K L : CochainComplex C â„¤} {n m : â„¤}
  (Î± : Cochain K F m) (Î² : Cochain K G n) {n' m' : â„¤} (Î±' : Cochain F L m') (Î²' : Cochain G L n')
  (h : n + 1 = m) (h' : m' + 1 = n') (p : â„¤) (hp : n + n' = p)

@[simp]
lemma liftCochain_descCochain :
    (liftCochain Ï† Î± Î² h).comp (descCochain Ï† Î±' Î²' h') hp =
      Î±.comp Î±' (by omega) + Î².comp Î²' (by omega) := by
  simp [liftCochain, descCochain,
    Cochain.comp_assoc Î± (inl Ï†) _ _ (show -1 + n' = m' by omega) (by linarith)]

lemma liftCochain_v_descCochain_v (pâ‚ pâ‚‚ pâ‚ƒ : â„¤) (hâ‚â‚‚ : pâ‚ + n = pâ‚‚) (hâ‚‚â‚ƒ : pâ‚‚ + n' = pâ‚ƒ)
    (q : â„¤) (hq : pâ‚ + m = q) :
    (liftCochain Ï† Î± Î² h).v pâ‚ pâ‚‚ hâ‚â‚‚ â‰« (descCochain Ï† Î±' Î²' h').v pâ‚‚ pâ‚ƒ hâ‚‚â‚ƒ =
      Î±.v pâ‚ q hq â‰« Î±'.v q pâ‚ƒ (by omega) + Î².v pâ‚ pâ‚‚ hâ‚â‚‚ â‰« Î²'.v pâ‚‚ pâ‚ƒ hâ‚‚â‚ƒ := by
  have eq := Cochain.congr_v (liftCochain_descCochain Ï† Î± Î² Î±' Î²' h h' p hp) pâ‚ pâ‚ƒ (by omega)
  simpa only [Cochain.comp_v _ _ hp pâ‚ pâ‚‚ pâ‚ƒ hâ‚â‚‚ hâ‚‚â‚ƒ, Cochain.add_v,
    Cochain.comp_v _ _ _ _ _ _ hq (show q + m' = pâ‚ƒ by omega)] using eq

end

lemma lift_desc_f {K L : CochainComplex C â„¤} (Î± : Cocycle K F 1) (Î² : Cochain K G 0)
    (eq : Î´ 0 1 Î² + Î±.1.comp (Cochain.ofHom Ï†) (add_zero 1) = 0)
    (Î±' : Cochain F L (-1)) (Î²' : G âŸ¶ L)
    (eq' : Î´ (-1) 0 Î±' = Cochain.ofHom (Ï† â‰« Î²')) (n n' : â„¤) (hnn' : n + 1 = n') :
    (lift Ï† Î± Î² eq).f n â‰« (desc Ï† Î±' Î²' eq').f n =
    Î±.1.v n n' hnn' â‰« Î±'.v n' n (by omega) + Î².v n n (add_zero n) â‰« Î²'.f n := by
  simp only [lift, desc, Cocycle.homOf_f, liftCocycle_coe, descCocycle_coe, Cocycle.ofHom_coe,
    liftCochain_v_descCochain_v Ï† Î±.1 Î² Î±' (Cochain.ofHom Î²') (zero_add 1) (neg_add_self 1) 0
    (add_zero 0) n n n (add_zero n) (add_zero n) n' hnn', Cochain.ofHom_v]

end mappingCone

end CochainComplex
