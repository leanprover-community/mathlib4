/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Homology.HomotopyCategory
import Mathlib.Algebra.Ring.NegOnePow
import Mathlib.CategoryTheory.Shift.Quotient
import Mathlib.CategoryTheory.Linear.LinearFunctor
import Mathlib.Tactic.Linarith

/-!
# The shift on cochain complexes and on the homotopy category

In this file, we show that for any preadditive category `C`, the categories
`CochainComplex C ‚Ñ§` and `HomotopyCategory C (ComplexShape.up ‚Ñ§)` are
equipped with a shift by `‚Ñ§`.

We also show that if `F : C ‚•§ D` is an additive functor, then the functors
`F.mapHomologicalComplex (ComplexShape.up ‚Ñ§)` and
`F.mapHomotopyCategory (ComplexShape.up ‚Ñ§)` commute with the shift by `‚Ñ§`.

-/

assert_not_exists TwoSidedIdeal

universe v v' u u'

open CategoryTheory

variable (C : Type u) [Category.{v} C] [Preadditive C]
  {D : Type u'} [Category.{v'} D] [Preadditive D]

namespace CochainComplex

open HomologicalComplex

/-- The shift functor by `n : ‚Ñ§` on `CochainComplex C ‚Ñ§` which sends a cochain
complex `K` to the complex which is `K.X (i + n)` in degree `i`, and which
multiplies the differentials by `(-1)^n`. -/
@[simps]
def shiftFunctor (n : ‚Ñ§) : CochainComplex C ‚Ñ§ ‚•§ CochainComplex C ‚Ñ§ where
  obj K :=
    { X := fun i => K.X (i + n)
      d := fun _ _ => n.negOnePow ‚Ä¢ K.d _ _
      d_comp_d' := by
        intros
        simp only [Linear.comp_units_smul, Linear.units_smul_comp, d_comp_d, smul_zero]
      shape := fun i j hij => by
        rw [K.shape, smul_zero]
        intro hij'
        apply hij
        dsimp at hij' ‚ä¢
        omega }
  map œÜ :=
    { f := fun _ => œÜ.f _
      comm' := by
        intros
        dsimp
        simp only [Linear.comp_units_smul, Hom.comm, Linear.units_smul_comp] }
  map_id := by intros; rfl
  map_comp := by intros; rfl

instance (n : ‚Ñ§) : (shiftFunctor C n).Additive where

instance (n : ‚Ñ§) {R : Type*} [Ring R] [Linear R C] :
    Functor.Linear R (shiftFunctor C n) where

variable {C}

/-- The canonical isomorphism `((shiftFunctor C n).obj K).X i ‚âÖ K.X m` when `m = i + n`. -/
@[simp]
def shiftFunctorObjXIso (K : CochainComplex C ‚Ñ§) (n i m : ‚Ñ§) (hm : m = i + n) :
    ((shiftFunctor C n).obj K).X i ‚âÖ K.X m := K.XIsoOfEq hm.symm

section

variable (C)

attribute [local simp] XIsoOfEq_hom_naturality

/-- The shift functor by `n` on `CochainComplex C ‚Ñ§` identifies to the identity
functor when `n = 0`. -/
@[simps!]
def shiftFunctorZero' (n : ‚Ñ§) (h : n = 0) :
    shiftFunctor C n ‚âÖ ùü≠ _ :=
  NatIso.ofComponents (fun K => Hom.isoOfComponents
    (fun i => K.shiftFunctorObjXIso _ _ _ (by cutsat))
    (fun _ _ _ => by simp [h])) (fun _ ‚Ü¶ by ext; simp)

/-- The compatibility of the shift functors on `CochainComplex C ‚Ñ§` with respect
to the addition of integers. -/
@[simps!]
def shiftFunctorAdd' (n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§) (h : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ) :
    shiftFunctor C n‚ÇÅ‚ÇÇ ‚âÖ shiftFunctor C n‚ÇÅ ‚ãô shiftFunctor C n‚ÇÇ :=
  NatIso.ofComponents (fun K => Hom.isoOfComponents
    (fun i => K.shiftFunctorObjXIso _ _ _ (by cutsat))
    (fun _ _ _ => by
      subst h
      dsimp
      simp only [add_comm n‚ÇÅ n‚ÇÇ, Int.negOnePow_add, Linear.units_smul_comp,
        Linear.comp_units_smul, d_comp_XIsoOfEq_hom, smul_smul, XIsoOfEq_hom_comp_d]))
    (by intros; ext; simp)

attribute [local simp] XIsoOfEq

instance : HasShift (CochainComplex C ‚Ñ§) ‚Ñ§ := hasShiftMk _ _
  { F := shiftFunctor C
    zero := shiftFunctorZero' C _ rfl
    add := fun n‚ÇÅ n‚ÇÇ => shiftFunctorAdd' C n‚ÇÅ n‚ÇÇ _ rfl }

instance (n : ‚Ñ§) :
    (CategoryTheory.shiftFunctor (HomologicalComplex C (ComplexShape.up ‚Ñ§)) n).Additive :=
  (inferInstance : (CochainComplex.shiftFunctor C n).Additive)

instance (n : ‚Ñ§) {R : Type*} [Ring R] [Linear R C] :
    Functor.Linear R
      (CategoryTheory.shiftFunctor (HomologicalComplex C (ComplexShape.up ‚Ñ§)) n) where

end

@[simp]
lemma shiftFunctor_obj_X' (K : CochainComplex C ‚Ñ§) (n p : ‚Ñ§) :
    ((CategoryTheory.shiftFunctor (CochainComplex C ‚Ñ§) n).obj K).X p = K.X (p + n) := rfl

@[simp]
lemma shiftFunctor_map_f' {K L : CochainComplex C ‚Ñ§} (œÜ : K ‚ü∂ L) (n p : ‚Ñ§) :
    ((CategoryTheory.shiftFunctor (CochainComplex C ‚Ñ§) n).map œÜ).f p = œÜ.f (p + n) := rfl

@[simp]
lemma shiftFunctor_obj_d' (K : CochainComplex C ‚Ñ§) (n i j : ‚Ñ§) :
    ((CategoryTheory.shiftFunctor (CochainComplex C ‚Ñ§) n).obj K).d i j =
      n.negOnePow ‚Ä¢ K.d _ _ := rfl

lemma shiftFunctorAdd_inv_app_f (K : CochainComplex C ‚Ñ§) (a b n : ‚Ñ§) :
    ((shiftFunctorAdd (CochainComplex C ‚Ñ§) a b).inv.app K).f n =
      (K.XIsoOfEq (by dsimp; rw [add_comm a, add_assoc])).hom := rfl

lemma shiftFunctorAdd_hom_app_f (K : CochainComplex C ‚Ñ§) (a b n : ‚Ñ§) :
    ((shiftFunctorAdd (CochainComplex C ‚Ñ§) a b).hom.app K).f n =
      (K.XIsoOfEq (by dsimp; rw [add_comm a, add_assoc])).hom := by
  tauto

lemma shiftFunctorAdd'_inv_app_f' (K : CochainComplex C ‚Ñ§) (a b ab : ‚Ñ§) (h : a + b = ab) (n : ‚Ñ§) :
    ((CategoryTheory.shiftFunctorAdd' (CochainComplex C ‚Ñ§) a b ab h).inv.app K).f n =
      (K.XIsoOfEq (by dsimp; rw [‚Üê h, add_assoc, add_comm a])).hom := by
  subst h
  rw [shiftFunctorAdd'_eq_shiftFunctorAdd, shiftFunctorAdd_inv_app_f]

lemma shiftFunctorAdd'_hom_app_f' (K : CochainComplex C ‚Ñ§) (a b ab : ‚Ñ§) (h : a + b = ab) (n : ‚Ñ§) :
    ((CategoryTheory.shiftFunctorAdd' (CochainComplex C ‚Ñ§) a b ab h).hom.app K).f n =
      (K.XIsoOfEq (by dsimp; rw [‚Üê h, add_assoc, add_comm a])).hom := by
  subst h
  rw [shiftFunctorAdd'_eq_shiftFunctorAdd, shiftFunctorAdd_hom_app_f]

lemma shiftFunctorZero_inv_app_f (K : CochainComplex C ‚Ñ§) (n : ‚Ñ§) :
    ((CategoryTheory.shiftFunctorZero (CochainComplex C ‚Ñ§) ‚Ñ§).inv.app K).f n =
      (K.XIsoOfEq (by dsimp; rw [add_zero])).hom := rfl

lemma shiftFunctorZero_hom_app_f (K : CochainComplex C ‚Ñ§) (n : ‚Ñ§) :
    ((CategoryTheory.shiftFunctorZero (CochainComplex C ‚Ñ§) ‚Ñ§).hom.app K).f n =
      (K.XIsoOfEq (by dsimp; rw [add_zero])).hom := by
  tauto

lemma XIsoOfEq_shift (K : CochainComplex C ‚Ñ§) (n : ‚Ñ§) {p q : ‚Ñ§} (hpq : p = q) :
    (K‚ü¶n‚üß).XIsoOfEq hpq = K.XIsoOfEq (show p + n = q + n by rw [hpq]) := rfl

variable (C)

lemma shiftFunctorAdd'_eq (a b c : ‚Ñ§) (h : a + b = c) :
    CategoryTheory.shiftFunctorAdd' (CochainComplex C ‚Ñ§) a b c h =
      shiftFunctorAdd' C a b c h := by
  ext
  simp only [shiftFunctorAdd'_hom_app_f', XIsoOfEq, eqToIso.hom, shiftFunctorAdd'_hom_app_f]

lemma shiftFunctorAdd_eq (a b : ‚Ñ§) :
    CategoryTheory.shiftFunctorAdd (CochainComplex C ‚Ñ§) a b = shiftFunctorAdd' C a b _ rfl := by
  rw [‚Üê CategoryTheory.shiftFunctorAdd'_eq_shiftFunctorAdd, shiftFunctorAdd'_eq]

lemma shiftFunctorZero_eq :
    CategoryTheory.shiftFunctorZero (CochainComplex C ‚Ñ§) ‚Ñ§ = shiftFunctorZero' C 0 rfl := by
  ext
  rw [shiftFunctorZero_hom_app_f, shiftFunctorZero'_hom_app_f]

variable {C}

lemma shiftFunctorComm_hom_app_f (K : CochainComplex C ‚Ñ§) (a b p : ‚Ñ§) :
    ((shiftFunctorComm (CochainComplex C ‚Ñ§) a b).hom.app K).f p =
      (K.XIsoOfEq (show p + b + a = p + a + b
        by rw [add_assoc, add_comm b, add_assoc])).hom := by
  rw [shiftFunctorComm_eq _ _ _ _ rfl]
  dsimp
  rw [shiftFunctorAdd'_inv_app_f', shiftFunctorAdd'_hom_app_f']
  simp only [XIsoOfEq, eqToIso.hom, eqToHom_trans]

variable (C)

attribute [local simp] XIsoOfEq_hom_naturality

/-- Shifting cochain complexes by `n` and evaluating in a degree `i` identifies
to the evaluation in degree `i'` when `n + i = i'`. -/
@[simps!]
def shiftEval (n i i' : ‚Ñ§) (hi : n + i = i') :
    (CategoryTheory.shiftFunctor (CochainComplex C ‚Ñ§) n) ‚ãô
      HomologicalComplex.eval C (ComplexShape.up ‚Ñ§) i ‚âÖ
      HomologicalComplex.eval C (ComplexShape.up ‚Ñ§) i' :=
  NatIso.ofComponents (fun K => K.XIsoOfEq (by dsimp; rw [‚Üê hi, add_comm i]))
    (by simp)

end CochainComplex

namespace CategoryTheory

open Category

namespace Functor

variable {C}
variable (F : C ‚•§ D) [F.Additive]

attribute [local simp] Functor.map_zsmul

/-- The commutation with the shift isomorphism for the functor on cochain complexes
induced by an additive functor between preadditive categories. -/
@[simps!]
def mapCochainComplexShiftIso (n : ‚Ñ§) :
    shiftFunctor _ n ‚ãô F.mapHomologicalComplex (ComplexShape.up ‚Ñ§) ‚âÖ
      F.mapHomologicalComplex (ComplexShape.up ‚Ñ§) ‚ãô shiftFunctor _ n :=
  NatIso.ofComponents (fun K => HomologicalComplex.Hom.isoOfComponents (fun _ => Iso.refl _)
    (by simp)) (fun _ => by ext; dsimp; rw [id_comp, comp_id])

instance commShiftMapCochainComplex :
    (F.mapHomologicalComplex (ComplexShape.up ‚Ñ§)).CommShift ‚Ñ§ where
  iso := F.mapCochainComplexShiftIso
  zero := by
    ext
    rw [CommShift.isoZero_hom_app]
    dsimp
    simp only [CochainComplex.shiftFunctorZero_inv_app_f, CochainComplex.shiftFunctorZero_hom_app_f,
       HomologicalComplex.XIsoOfEq, eqToIso, eqToHom_map, eqToHom_trans, eqToHom_refl]
  add := fun a b => by
    ext
    rw [CommShift.isoAdd_hom_app]
    dsimp
    rw [id_comp, id_comp]
    simp only [CochainComplex.shiftFunctorAdd_hom_app_f,
      CochainComplex.shiftFunctorAdd_inv_app_f, HomologicalComplex.XIsoOfEq, eqToIso,
      eqToHom_map, eqToHom_trans, eqToHom_refl]

lemma mapHomologicalComplex_commShiftIso_eq (n : ‚Ñ§) :
    (F.mapHomologicalComplex (ComplexShape.up ‚Ñ§)).commShiftIso n =
      F.mapCochainComplexShiftIso n := rfl

@[simp]
lemma mapHomologicalComplex_commShiftIso_hom_app_f (K : CochainComplex C ‚Ñ§) (n i : ‚Ñ§) :
    (((F.mapHomologicalComplex (ComplexShape.up ‚Ñ§)).commShiftIso n).hom.app K).f i = ùüô _ := rfl

@[simp]
lemma mapHomologicalComplex_commShiftIso_inv_app_f (K : CochainComplex C ‚Ñ§) (n i : ‚Ñ§) :
    (((F.mapHomologicalComplex (ComplexShape.up ‚Ñ§)).commShiftIso n).inv.app K).f i = ùüô _ := rfl

end Functor

end CategoryTheory

namespace Homotopy

variable {C}

/-- If `h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ` and `n : ‚Ñ§`, this is the induced homotopy
between `œÜ‚ÇÅ‚ü¶n‚üß'` and `œÜ‚ÇÇ‚ü¶n‚üß'`. -/
def shift {K L : CochainComplex C ‚Ñ§} {œÜ‚ÇÅ œÜ‚ÇÇ : K ‚ü∂ L} (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) (n : ‚Ñ§) :
    Homotopy (œÜ‚ÇÅ‚ü¶n‚üß') (œÜ‚ÇÇ‚ü¶n‚üß') where
  hom _ _ := n.negOnePow ‚Ä¢ h.hom _ _
  zero i j hij := by
    dsimp
    rw [h.zero, smul_zero]
    intro hij'
    dsimp at hij hij'
    omega
  comm := fun i => by
    rw [dNext_eq _ (show (ComplexShape.up ‚Ñ§).Rel i (i + 1) by simp),
      prevD_eq _ (show (ComplexShape.up ‚Ñ§).Rel (i - 1) i by simp)]
    dsimp
    simpa only [Linear.units_smul_comp, Linear.comp_units_smul, smul_smul,
      Int.units_mul_self, one_smul,
      dNext_eq _ (show (ComplexShape.up ‚Ñ§).Rel (i + n) (i + 1 + n) by dsimp; omega),
      prevD_eq _ (show (ComplexShape.up ‚Ñ§).Rel (i - 1 + n) (i + n) by dsimp; omega)]
        using h.comm (i + n)

end Homotopy

namespace HomotopyCategory

instance : (homotopic C (ComplexShape.up ‚Ñ§)).IsCompatibleWithShift ‚Ñ§ :=
  ‚ü®fun n _ _ _ _ ‚ü®h‚ü© => ‚ü®h.shift n‚ü©‚ü©

noncomputable instance hasShift :
    HasShift (HomotopyCategory C (ComplexShape.up ‚Ñ§)) ‚Ñ§ := by
  dsimp only [HomotopyCategory]
  infer_instance

noncomputable instance commShiftQuotient :
    (HomotopyCategory.quotient C (ComplexShape.up ‚Ñ§)).CommShift ‚Ñ§ :=
  Quotient.functor_commShift (homotopic C (ComplexShape.up ‚Ñ§)) ‚Ñ§

instance (n : ‚Ñ§) : (shiftFunctor (HomotopyCategory C (ComplexShape.up ‚Ñ§)) n).Additive := by
  have : ((quotient C (ComplexShape.up ‚Ñ§) ‚ãô shiftFunctor _ n)).Additive :=
    Functor.additive_of_iso ((quotient C (ComplexShape.up ‚Ñ§)).commShiftIso n)
  apply Functor.additive_of_full_essSurj_comp (quotient _ _ )

instance {R : Type*} [Ring R] [CategoryTheory.Linear R C] (n : ‚Ñ§) :
    (CategoryTheory.shiftFunctor (HomotopyCategory C (ComplexShape.up ‚Ñ§)) n).Linear R where
  map_smul := by
    rintro ‚ü®X‚ü© ‚ü®Y‚ü© f r
    obtain ‚ü®f, rfl‚ü© := (HomotopyCategory.quotient C (ComplexShape.up ‚Ñ§)).map_surjective f
    have h‚ÇÅ := NatIso.naturality_1 ((HomotopyCategory.quotient _ _).commShiftIso n) f
    have h‚ÇÇ := NatIso.naturality_1 ((HomotopyCategory.quotient _ _).commShiftIso n) (r ‚Ä¢ f)
    dsimp at h‚ÇÅ h‚ÇÇ
    rw [‚Üê Functor.map_smul, ‚Üê h‚ÇÅ, ‚Üê h‚ÇÇ]
    simp

section

variable {C}
variable (F : C ‚•§ D) [F.Additive]

noncomputable instance : (F.mapHomotopyCategory (ComplexShape.up ‚Ñ§)).CommShift ‚Ñ§ :=
  Quotient.liftCommShift _ _ _ _

instance : NatTrans.CommShift (F.mapHomotopyCategoryFactors (ComplexShape.up ‚Ñ§)).hom ‚Ñ§ :=
  Quotient.liftCommShift_compatibility _ _ _ _

end

end HomotopyCategory
