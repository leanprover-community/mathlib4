/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Homology.Additive
import Mathlib.Algebra.GroupPower.NegOnePow
import Mathlib.Tactic.Linarith

/-!
# The shift on cochain complexes and on the homotopy category

In this file, we show `[HasShift (CochainComplex C ‚Ñ§) ‚Ñ§]` for any preadditive
category `C`.

TODO: show `[HasShift (HomotopyCategory C (ComplexShape.up ‚Ñ§)) ‚Ñ§]`.

-/

universe v u

open CategoryTheory

variable (C : Type u) [Category.{v} C] [Preadditive C]

namespace CochainComplex

open HomologicalComplex

attribute [local simp] XIsoOfEq_hom_naturality

/-- The shift functor by `n : ‚Ñ§` on `CochainComplex C ‚Ñ§` which sends a cochain
complex `K` to the complex which is `K.X (i + n)` in degree `i`, and which
multiplies the differentials by `(-1)^n`. -/
@[simps]
def shiftFunctor (n : ‚Ñ§) : CochainComplex C ‚Ñ§ ‚•§ CochainComplex C ‚Ñ§ where
  obj K :=
    { X := fun i => K.X (i + n)
      d := fun i j => n.negOnePow ‚Ä¢ K.d _ _
      d_comp_d' := by
        intros
        simp only [Preadditive.comp_zsmul, Preadditive.zsmul_comp, d_comp_d, smul_zero]
      shape := fun i j hij => by
        dsimp
        rw [K.shape, smul_zero]
        intro hij'
        apply hij
        dsimp at hij' ‚ä¢
        linarith }
  map œÜ :=
    { f := fun i => œÜ.f _
      comm' := by
        intros
        dsimp
        simp only [Preadditive.comp_zsmul, Hom.comm, Preadditive.zsmul_comp] }
  map_id := by intros; rfl
  map_comp := by intros; rfl

instance (n : ‚Ñ§) : (shiftFunctor C n).Additive where

variable {C}

/-- The canonical isomorphism `((shiftFunctor C n).obj K).X i ‚âÖ K.X m` when `m = i + n`. -/
@[simp]
def shiftFunctorObjXIso (K : CochainComplex C ‚Ñ§) (n i m : ‚Ñ§) (hm : m = i + n) :
    ((shiftFunctor C n).obj K).X i ‚âÖ K.X m := K.XIsoOfEq hm.symm

variable (C)

/-- The shift functor by `n` on `CochainComplex C ‚Ñ§` identifies to the identity
functor when `n = 0`. -/
@[simps!]
def shiftFunctorZero' (n : ‚Ñ§) (h : n = 0) :
    shiftFunctor C n ‚âÖ ùü≠ _ :=
  NatIso.ofComponents (fun K => Hom.isoOfComponents
    (fun i => K.shiftFunctorObjXIso _ _ _ (by linarith))
    (fun _ _ _ => by simp [h])) (by aesop_cat)

/-- The compatibility of the shift functors on `CochainComplex C ‚Ñ§` with respect
to the addition of integers. -/
@[simps!]
def shiftFunctorAdd' (n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§) (h : n‚ÇÅ + n‚ÇÇ = n‚ÇÅ‚ÇÇ ) :
    shiftFunctor C n‚ÇÅ‚ÇÇ ‚âÖ shiftFunctor C n‚ÇÅ ‚ãô shiftFunctor C n‚ÇÇ :=
  NatIso.ofComponents (fun K => Hom.isoOfComponents
    (fun i => K.shiftFunctorObjXIso _ _ _ (by linarith))
    (fun _ _ _ => by
      subst h
      dsimp
      simp only [add_comm n‚ÇÅ n‚ÇÇ, Int.negOnePow_add, Preadditive.zsmul_comp,
        Preadditive.comp_zsmul, d_comp_XIsoOfEq_hom, smul_smul, XIsoOfEq_hom_comp_d]))
    (by aesop_cat)

attribute [local simp] XIsoOfEq

instance : HasShift (CochainComplex C ‚Ñ§) ‚Ñ§ := hasShiftMk _ _
  { F := shiftFunctor C
    zero := shiftFunctorZero' C _ rfl
    add := fun n‚ÇÅ n‚ÇÇ => shiftFunctorAdd' C n‚ÇÅ n‚ÇÇ _ rfl }

instance (n : ‚Ñ§) :
    (CategoryTheory.shiftFunctor (HomologicalComplex C (ComplexShape.up ‚Ñ§)) n).Additive :=
  (inferInstance : (CochainComplex.shiftFunctor C n).Additive)

variable {C}

@[simp]
lemma shiftFunctor_map_f' {K L : CochainComplex C ‚Ñ§} (œÜ : K ‚ü∂ L) (n p : ‚Ñ§) :
    ((CategoryTheory.shiftFunctor (CochainComplex C ‚Ñ§) n).map œÜ).f p = œÜ.f (p+n) := rfl

@[simp]
lemma shiftFunctor_obj_d' (K : CochainComplex C ‚Ñ§) (n i j : ‚Ñ§) :
    ((CategoryTheory.shiftFunctor (CochainComplex C ‚Ñ§) n).obj K).d i j =
      ((-1 : Units ‚Ñ§) ^ n : ‚Ñ§) ‚Ä¢ K.d _ _ := rfl

lemma shiftFunctorAdd_inv_app_f (K : CochainComplex C ‚Ñ§) (a b n : ‚Ñ§) :
    ((shiftFunctorAdd (CochainComplex C ‚Ñ§) a b).inv.app K).f n =
      (K.XIsoOfEq (by dsimp; rw [add_comm a, add_assoc])).hom := rfl

lemma shiftFunctorAdd_hom_app_f (K : CochainComplex C ‚Ñ§) (a b n : ‚Ñ§) :
    ((shiftFunctorAdd (CochainComplex C ‚Ñ§) a b).hom.app K).f n =
      (K.XIsoOfEq (by dsimp; rw [add_comm a, add_assoc])).hom := by
  have : IsIso (((shiftFunctorAdd (CochainComplex C ‚Ñ§) a b).inv.app K).f n) := by
    rw [shiftFunctorAdd_inv_app_f]
    infer_instance
  rw [‚Üê cancel_mono (((shiftFunctorAdd (CochainComplex C ‚Ñ§) a b).inv.app K).f n),
    ‚Üê comp_f, Iso.hom_inv_id_app, id_f, shiftFunctorAdd_inv_app_f]
  simp only [XIsoOfEq, eqToIso.hom, eqToHom_trans, eqToHom_refl]

lemma shiftFunctorAdd'_inv_app_f' (K : CochainComplex C ‚Ñ§) (a b ab : ‚Ñ§) (h : a + b = ab) (n : ‚Ñ§) :
    ((CategoryTheory.shiftFunctorAdd' (CochainComplex C ‚Ñ§) a b ab h).inv.app K).f n =
      (K.XIsoOfEq (by dsimp; rw [‚Üê h, add_assoc, add_comm a])).hom := by
  subst h
  rw [shiftFunctorAdd'_eq_shiftFunctorAdd, shiftFunctorAdd_inv_app_f]

lemma shiftFunctorAdd'_hom_app_f' (K : CochainComplex C ‚Ñ§) (a b ab : ‚Ñ§) (h : a + b = ab) (n : ‚Ñ§) :
    ((CategoryTheory.shiftFunctorAdd' (CochainComplex C ‚Ñ§) a b ab h).hom.app K).f n =
      (K.XIsoOfEq (by dsimp; rw [‚Üê h, add_assoc, add_comm a])).hom := by
  subst h
  rw [shiftFunctorAdd'_eq_shiftFunctorAdd, shiftFunctorAdd_hom_app_f]

lemma shiftFunctorZero_inv_app_f (K : CochainComplex C ‚Ñ§) (n : ‚Ñ§) :
    ((CategoryTheory.shiftFunctorZero (CochainComplex C ‚Ñ§) ‚Ñ§).inv.app K).f n =
      (K.XIsoOfEq (by dsimp; rw [add_zero])).hom := rfl

lemma shiftFunctorZero_hom_app_f (K : CochainComplex C ‚Ñ§) (n : ‚Ñ§) :
    ((CategoryTheory.shiftFunctorZero (CochainComplex C ‚Ñ§) ‚Ñ§).hom.app K).f n =
      (K.XIsoOfEq (by dsimp; rw [add_zero])).hom := by
  have : IsIso (((shiftFunctorZero (CochainComplex C ‚Ñ§) ‚Ñ§).inv.app K).f n) := by
    rw [shiftFunctorZero_inv_app_f]
    infer_instance
  rw [‚Üê cancel_mono (((shiftFunctorZero (CochainComplex C ‚Ñ§) ‚Ñ§).inv.app K).f n), ‚Üê comp_f,
    Iso.hom_inv_id_app, id_f, shiftFunctorZero_inv_app_f]
  simp only [XIsoOfEq, eqToIso.hom, eqToHom_trans, eqToHom_refl]

lemma XIsoOfEq_shift (K : CochainComplex C ‚Ñ§) (n : ‚Ñ§) {p q : ‚Ñ§} (hpq : p = q) :
    (K‚ü¶n‚üß).XIsoOfEq hpq = K.XIsoOfEq (show p + n = q + n by rw [hpq]) := rfl

variable (C)

lemma shiftFunctorAdd'_eq (a b c : ‚Ñ§) (h : a + b = c) :
    CategoryTheory.shiftFunctorAdd' (CochainComplex C ‚Ñ§) a b c h =
      shiftFunctorAdd' C a b c h := by
  ext
  simp only [shiftFunctorAdd'_hom_app_f', XIsoOfEq, eqToIso.hom, shiftFunctorAdd'_hom_app_f]

lemma shiftFunctorAdd_eq (a b : ‚Ñ§) :
    CategoryTheory.shiftFunctorAdd (CochainComplex C ‚Ñ§) a b = shiftFunctorAdd' C a b _ rfl := by
  rw [‚Üê CategoryTheory.shiftFunctorAdd'_eq_shiftFunctorAdd, shiftFunctorAdd'_eq]

lemma shiftFunctorZero_eq :
    CategoryTheory.shiftFunctorZero (CochainComplex C ‚Ñ§) ‚Ñ§ = shiftFunctorZero' C 0 rfl := by
  ext
  rw [shiftFunctorZero_hom_app_f, shiftFunctorZero'_hom_app_f]

variable {C}

lemma shiftFunctorComm_hom_app_f (K : CochainComplex C ‚Ñ§) (a b p : ‚Ñ§) :
    ((shiftFunctorComm (CochainComplex C ‚Ñ§) a b).hom.app K).f p =
      (K.XIsoOfEq (show p + b + a = p + a + b
        by rw [add_assoc, add_comm b, add_assoc])).hom := by
  rw [shiftFunctorComm_eq _ _ _ _ rfl]
  dsimp
  rw [shiftFunctorAdd'_inv_app_f', shiftFunctorAdd'_hom_app_f']
  simp only [XIsoOfEq, eqToIso.hom, eqToHom_trans]

end CochainComplex
