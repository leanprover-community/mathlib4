import Mathlib.Algebra.Homology.HomotopyCategory.MappingCone
import Mathlib.CategoryTheory.Triangulated.Functor
import Mathlib.CategoryTheory.Triangulated.Pretriangulated
import Mathlib.CategoryTheory.Triangulated.TriangleShift
import Mathlib.Algebra.EuclideanDomain.Basic
import Mathlib.Algebra.EuclideanDomain.Instances

open CategoryTheory Category Limits CochainComplex.HomComplex Pretriangulated ZeroObject
  Preadditive

variable {C : Type _} [Category C] [Preadditive C] [HasZeroObject C] [HasBinaryBiproducts C]
variable {K L : CochainComplex C ‚Ñ§} (œÜ : K ‚ü∂ L)

namespace CochainComplex

namespace MappingCone

noncomputable def triangleŒ¥ : mappingCone œÜ ‚ü∂ K‚ü¶(1 : ‚Ñ§)‚üß :=
  Cocycle.homOf ((-fst œÜ).rightShift 1 0 (zero_add 1))

@[reassoc (attr := simp)]
lemma inl_v_triangleŒ¥_f (p q : ‚Ñ§) (hpq : p + (-1) = q) :
    (inl œÜ : Cochain K (mappingCone œÜ) (-1)).v p q hpq ‚â´ (triangleŒ¥ œÜ).f q =
      -(K.shiftFunctorObjXIso 1 q p (by rw [‚Üê hpq, neg_add_cancel_right])).inv := by
  dsimp only [triangleŒ¥]
  simp only [Cocycle.homOf_f, Cocycle.rightShift_coe, Cocycle.coe_neg,
    Cochain.rightShift_neg, Cochain.neg_v, comp_neg, shiftFunctor_obj_X, shiftFunctorObjXIso,
    Cochain.rightShift_v _ 1 0 (zero_add 1) q q (add_zero q) p (by linarith), inl_v_fst_v_assoc]

@[reassoc (attr := simp)]
lemma inr_f_triangleŒ¥_f (p : ‚Ñ§) : (inr œÜ).f p ‚â´ (triangleŒ¥ œÜ).f p = 0 := by
  dsimp [triangleŒ¥]
  simp only [Cochain.rightShift_v _ 1 0 _ p p (add_zero p) (p+1) rfl, Cochain.neg_v,
    comp_neg, neg_comp, inr_f_fst_v_assoc, zero_comp, neg_zero]

@[simp]
lemma inr_triangleŒ¥ : inr œÜ ‚â´ triangleŒ¥ œÜ = 0 := by aesop_cat

@[simp]
noncomputable def triangle : Triangle (CochainComplex C ‚Ñ§) :=
  Triangle.mk œÜ (inr œÜ) (triangleŒ¥ œÜ)

variable (K)

noncomputable def homotopyToZeroOfId : Homotopy (ùüô (mappingCone (ùüô K))) 0 :=
  descHomotopy (ùüô K) _ _ 0 (inl _) (by simp) (by simp)

variable {K}

section map

variable {K‚ÇÅ L‚ÇÅ K‚ÇÇ L‚ÇÇ K‚ÇÉ L‚ÇÉ : CochainComplex C ‚Ñ§} {œÜ‚ÇÅ : K‚ÇÅ ‚ü∂ L‚ÇÅ} {œÜ‚ÇÇ : K‚ÇÇ ‚ü∂ L‚ÇÇ} (œÜ‚ÇÉ : K‚ÇÉ ‚ü∂ L‚ÇÉ)
  {a : K‚ÇÅ ‚ü∂ K‚ÇÇ} {b : L‚ÇÅ ‚ü∂ L‚ÇÇ} (H : Homotopy (œÜ‚ÇÅ ‚â´ b) (a ‚â´ œÜ‚ÇÇ))
  (a' : K‚ÇÇ ‚ü∂ K‚ÇÉ) (b' : L‚ÇÇ ‚ü∂ L‚ÇÉ)

noncomputable def map : mappingCone œÜ‚ÇÅ ‚ü∂ mappingCone œÜ‚ÇÇ :=
  desc œÜ‚ÇÅ ((Cochain.ofHom a).comp (inl œÜ‚ÇÇ) (zero_add _) +
      ((Cochain.equivHomotopy _ _) H : Cochain K‚ÇÅ L‚ÇÇ (-1)).comp
    (Cochain.ofHom (inr œÜ‚ÇÇ)) (add_zero _)) (b ‚â´ inr œÜ‚ÇÇ) (by simp)

@[reassoc]
lemma triangleMap_comm‚ÇÇ : inr œÜ‚ÇÅ ‚â´ map H = b ‚â´ inr œÜ‚ÇÇ := by
  simp only [map, Cochain.equivHomotopy_apply_coe, inr_desc]

@[reassoc]
lemma triangleMap_comm‚ÇÉ : map H ‚â´ triangleŒ¥ œÜ‚ÇÇ = triangleŒ¥ œÜ‚ÇÅ ‚â´ a‚ü¶1‚üß' := by
  ext p
  rw [from_ext_iff _ _ _ _ rfl]
  dsimp [triangleŒ¥, map]
  simp only [Cochain.rightShift_v _ 1 0 _ p p _ (p+1) rfl,
    shiftFunctor_obj_X, Cochain.neg_v, shiftFunctorObjXIso,
    HomologicalComplex.XIsoOfEq_rfl, Iso.refl_inv, comp_id, neg_comp,
    comp_neg, inl_v_fst_v_assoc, inl_v_desc_f_assoc,
    Cochain.add_v, Cochain.zero_cochain_comp_v, Cochain.ofHom_v, Cochain.comp_zero_cochain_v,
    add_comp, assoc, inl_v_fst_v, inr_f_fst_v, comp_zero, add_zero,
    inr_f_fst_v_assoc, zero_comp, neg_zero, inr_f_desc_f_assoc,
    HomologicalComplex.comp_f, and_self]

@[simps]
noncomputable def triangleMap :
    (HomotopyCategory.quotient _ _).mapTriangle.obj (CochainComplex.MappingCone.triangle œÜ‚ÇÅ) ‚ü∂
    (HomotopyCategory.quotient _ _).mapTriangle.obj (CochainComplex.MappingCone.triangle œÜ‚ÇÇ) where
  hom‚ÇÅ := (HomotopyCategory.quotient _ _).map a
  hom‚ÇÇ := (HomotopyCategory.quotient _ _).map b
  hom‚ÇÉ := (HomotopyCategory.quotient _ _).map (map H)
  comm‚ÇÅ := by
    dsimp
    simp only [‚Üê Functor.map_comp]
    exact HomotopyCategory.eq_of_homotopy _ _ H
  comm‚ÇÇ := by
    dsimp
    simp only [‚Üê Functor.map_comp, triangleMap_comm‚ÇÇ]
  comm‚ÇÉ := by
    dsimp
    rw [‚Üê Functor.map_comp_assoc, triangleMap_comm‚ÇÉ, Functor.map_comp, assoc, assoc]
    erw [‚Üê NatTrans.naturality]
    rfl

variable (œÜ‚ÇÅ œÜ‚ÇÇ a b)
variable (comm : œÜ‚ÇÅ ‚â´ b = a ‚â´ œÜ‚ÇÇ) (comm' : œÜ‚ÇÇ ‚â´ b' = a' ‚â´ œÜ‚ÇÉ)

noncomputable def map' : mappingCone œÜ‚ÇÅ ‚ü∂ mappingCone œÜ‚ÇÇ :=
  desc œÜ‚ÇÅ ((Cochain.ofHom a).comp (inl œÜ‚ÇÇ) (zero_add _)) (b ‚â´ inr œÜ‚ÇÇ)
    (by simp only [Œ¥_ofHom_comp, Œ¥_inl, reassoc_of% comm, Cochain.ofHom_comp])

lemma map'_eq_map : map' œÜ‚ÇÅ œÜ‚ÇÇ a b comm = map (Homotopy.ofEq comm) := by
  dsimp only [map, map']
  simp

lemma map'_id : map' œÜ œÜ (ùüô _) (ùüô _) (by rw [id_comp, comp_id]) = ùüô _ := by
  ext n
  simp [from_ext_iff _ _ _ (n+1) rfl, map']

lemma map'_comp : map' œÜ‚ÇÅ œÜ‚ÇÉ (a ‚â´ a') (b ‚â´ b') (by rw [reassoc_of% comm, comm', assoc]) =
    map' œÜ‚ÇÅ œÜ‚ÇÇ a b comm ‚â´ map' œÜ‚ÇÇ œÜ‚ÇÉ a' b' comm' := by
  ext n
  simp [from_ext_iff _ _ _ (n+1) rfl, map']

noncomputable def arrowFunctor : Arrow (CochainComplex C ‚Ñ§) ‚•§ CochainComplex C ‚Ñ§ where
  obj f := mappingCone f.hom
  map {f‚ÇÅ f‚ÇÇ} œÜ := map' f‚ÇÅ.hom f‚ÇÇ.hom œÜ.left œÜ.right œÜ.w.symm
  map_id f := map'_id f.hom
  map_comp {f‚ÇÅ f‚ÇÇ f‚ÇÉ}  œÜ‚ÇÅ œÜ‚ÇÇ := map'_comp f‚ÇÅ.hom f‚ÇÇ.hom f‚ÇÉ.hom œÜ‚ÇÅ.left œÜ‚ÇÅ.right
    œÜ‚ÇÇ.left œÜ‚ÇÇ.right œÜ‚ÇÅ.w.symm œÜ‚ÇÇ.w.symm

@[simps]
noncomputable def triangleMap' :
    CochainComplex.MappingCone.triangle œÜ‚ÇÅ ‚ü∂ CochainComplex.MappingCone.triangle œÜ‚ÇÇ where
  hom‚ÇÅ := a
  hom‚ÇÇ := b
  hom‚ÇÉ := map' _ _ _ _ comm
  comm‚ÇÅ := comm
  comm‚ÇÇ := by dsimp [triangle, map'] ; simp only [inr_desc]
  comm‚ÇÉ := by dsimp ; simp only [map'_eq_map, triangleMap_comm‚ÇÉ]

end map

section rotate

noncomputable def rotateHomotopyEquiv :
  HomotopyEquiv (K‚ü¶(1 : ‚Ñ§)‚üß) (mappingCone (inr œÜ)) where
  hom := lift (inr œÜ) (-(Cocycle.ofHom œÜ).leftShift 1 1 (zero_add 1))
    (-(inl œÜ).leftShift 1 0 (neg_add_self 1)) (by
      simp only [Œ¥_neg, Cocycle.coe_neg, Cocycle.leftShift_coe, Cochain.neg_comp,
        Cochain.Œ¥_leftShift _ 1 0 1 (neg_add_self 1) 0 (zero_add 1), Œµ_1, neg_smul, one_smul,
        neg_neg, Œ¥_inl, Cochain.leftShift_comp_zero_cochain, Cocycle.ofHom_coe,
        Cochain.ofHom_comp, add_right_neg])
  inv := desc (inr œÜ) 0 (triangleŒ¥ œÜ) (by simp)
  homotopyHomInvId := Homotopy.ofEq (by
    ext p
    simp only [HomologicalComplex.comp_f, HomologicalComplex.id_f,
      lift_desc_f _ _ _ _ _ _ _ _ _ rfl, Cochain.zero_v, comp_zero, zero_add,
      (inl œÜ).leftShift_v 1 0 (neg_add_self 1) p p (add_zero p) (p+1) (by linarith),
      mul_zero, sub_self, EuclideanDomain.zero_div, Œµ_0, one_smul, triangleŒ¥,
      Cocycle.homOf_f, Cocycle.rightShift_coe, Cocycle.coe_neg, Cochain.rightShift_neg,
      Cochain.neg_v, Cochain.rightShift_v _ 1 0 (zero_add 1) p p (add_zero p) (p+1) rfl,
      comp_neg, neg_comp, neg_neg, assoc, inl_v_fst_v_assoc, Iso.hom_inv_id])
  homotopyInvHomId := (Cochain.equivHomotopy _ _).symm
    ‚ü®-(snd (inr œÜ)).comp ((snd œÜ).comp (inl (inr œÜ)) (zero_add (-1))) (zero_add (-1)), by
      ext p
      simp only [Cochain.ofHom_comp, ofHom_desc, ofHom_lift, Cocycle.coe_neg,
        Cocycle.leftShift_coe, Cocycle.ofHom_coe, Cochain.zero_cochain_comp_v, Œ¥_neg,
        Cochain.add_v, Cochain.neg_v, Cochain.ofHom_v, HomologicalComplex.id_f,
        from_ext_iff _ _ _ _ rfl, to_ext_iff _ _ _ _ rfl,
        assoc, Œ¥_zero_cochain_comp _ _ _ (neg_add_self 1),
        Cochain.comp_v _ _ (add_neg_self 1) p (p + 1) p rfl (by linarith),
        Cochain.leftShift_v _ 1 1 (zero_add 1) p (p+1) rfl (p+1) (add_zero _),
        Cochain.leftShift_v _ 1 0 (neg_add_self 1) p p (add_zero p) (p+1) (by linarith),
        liftCochain_v_fst_v, comp_neg, inl_v_descCochain_v_assoc, Cochain.zero_v,
        zero_comp, neg_zero, Œ¥_inl, Cochain.ofHom_comp, Œµ_neg, Œµ_1, Œµ_0, Œ¥_snd,
        Cochain.neg_comp, Cochain.comp_assoc_of_second_is_zero_cochain, smul_neg, neg_smul,
        one_smul, neg_neg, Cochain.comp_add, inr_snd_assoc,
        Cochain.zero_cochain_comp_v, neg_add_rev, add_comp, neg_comp,
        inl_v_fst_v, comp_id, inr_f_fst_v, comp_zero, add_zero, id_comp, neg_add_cancel_comm,
        inl_v_snd_v_assoc, inr_f_descCochain_v_assoc, inr_f_snd_v_assoc, inl_v_fst_v_assoc,
        inr_f_fst_v_assoc, inr_f_triangleŒ¥_f_assoc, sub_self, one_mul,
        EuclideanDomain.zero_div, inl_v_triangleŒ¥_f_assoc,
        Iso.refl_inv, Iso.refl_hom, shiftFunctor_obj_X, shiftFunctorObjXIso,
        HomologicalComplex.XIsoOfEq_rfl, zero_add,
        liftCochain_v_snd_v_assoc, inr_f_snd_v, inl_v_snd_v, add_left_neg]‚ü©

noncomputable def rotateHomotopyEquivComm‚ÇÇHomotopy :
  Homotopy (triangleŒ¥ œÜ ‚â´ (rotateHomotopyEquiv œÜ).hom)
    (inr (CochainComplex.MappingCone.inr œÜ)) := (Cochain.equivHomotopy _ _).symm
      ‚ü®-(snd œÜ).comp ((inl (inr œÜ))) (zero_add (-1)), by
        ext p
        dsimp [rotateHomotopyEquiv]
        simp only [Cochain.ofHom_comp, Cochain.zero_cochain_comp_v, Cochain.ofHom_v,
          lift_f _ _ _ _ p (p+1) rfl,
          Cocycle.coe_neg, Cocycle.leftShift_coe, Cocycle.ofHom_coe, Cochain.neg_v,
          Cochain.leftShift_v _ 1 1 (zero_add 1) p (p + 1) rfl (p + 1) (add_zero _),
          Cochain.leftShift_v _ 1 0 (neg_add_self 1) p p (add_zero p) (p + 1) (by linarith),
          Œ¥_comp _ _ (zero_add (-1)) 1 0 0 (neg_add_self 1) (zero_add 1) (neg_add_self 1),
          Cochain.comp_v _ _ (add_neg_self 1) p (p + 1) p rfl (by linarith),
          from_ext_iff _ _ _ _ rfl, shiftFunctor_obj_X, mul_one, sub_self,
          mul_zero, EuclideanDomain.zero_div, add_zero, Œµ_1, shiftFunctorObjXIso,
          HomologicalComplex.XIsoOfEq_rfl, Iso.refl_hom, id_comp, neg_smul,
          one_smul, neg_neg, Œµ_0, neg_comp, comp_add, comp_neg, Œ¥_neg, Œ¥_inl,
          Œµ_neg, Œ¥_snd, Cochain.neg_comp, inl_v_triangleŒ¥_f_assoc, Iso.refl_inv,
          Cochain.comp_assoc_of_second_is_zero_cochain, smul_neg, neg_add_rev, Cochain.add_v,
          inl_v_fst_v_assoc, inl_v_snd_v_assoc, zero_comp, neg_zero, inr_f_triangleŒ¥_f_assoc,
          inr_f_fst_v_assoc, inr_f_snd_v_assoc, zero_add, add_left_neg, and_self]‚ü©

@[reassoc (attr := simp)]
lemma rotateHomotopyEquiv_comm‚ÇÇ :
    (HomotopyCategory.quotient _ _ ).map (triangleŒ¥ œÜ) ‚â´
      (HomotopyCategory.quotient _ _ ).map (rotateHomotopyEquiv œÜ).hom =
      (HomotopyCategory.quotient _ _ ).map (inr (inr œÜ)) := by
  simpa only [Functor.map_comp]
    using HomotopyCategory.eq_of_homotopy _ _  (rotateHomotopyEquivComm‚ÇÇHomotopy œÜ)

@[reassoc (attr := simp)]
lemma rotateHomotopyEquiv_comm‚ÇÉ :
    (rotateHomotopyEquiv œÜ).hom ‚â´ triangleŒ¥ (inr œÜ) = -œÜ‚ü¶1‚üß' := by
  ext p
  dsimp [rotateHomotopyEquiv]
  simp only [lift_f _ _ _ _ p (p+1) rfl, Cocycle.coe_neg, Cochain.neg_v,
    Cocycle.leftShift_coe, Cocycle.ofHom_coe, neg_comp, add_comp, assoc,
    inl_v_triangleŒ¥_f, shiftFunctor_obj_X, shiftFunctorObjXIso,
    HomologicalComplex.XIsoOfEq_rfl, Iso.refl_inv, comp_neg, comp_id, neg_neg,
    inr_f_triangleŒ¥_f, comp_zero, neg_zero, add_zero,
    Cochain.leftShift_v _ 1 1 (zero_add 1) p (p+1) rfl (p+1) (by linarith), mul_one,
    sub_self, EuclideanDomain.zero_div, one_mul, Œµ_1, neg_smul, one_smul, Iso.refl_hom,
    id_comp, Cochain.ofHom_v]

@[reassoc (attr := simp)]
lemma rotateHomotopyEquiv_comm‚ÇÉ' :
    (HomotopyCategory.quotient _ _).map (rotateHomotopyEquiv œÜ).hom ‚â´
      (HomotopyCategory.quotient _ _).map (triangleŒ¥ (inr œÜ)) =
      -(HomotopyCategory.quotient _ _).map (œÜ‚ü¶1‚üß') := by
  rw [‚Üê Functor.map_comp, rotateHomotopyEquiv_comm‚ÇÉ, Functor.map_neg]

end rotate

section shift

noncomputable def shiftIso (n : ‚Ñ§) : (mappingCone œÜ)‚ü¶n‚üß ‚âÖ mappingCone (œÜ‚ü¶n‚üß') where
  hom := lift _ (Œµ n ‚Ä¢ (fst œÜ).shift n) ((snd œÜ).shift n) (by
    ext ‚ü®p, q, hpq‚ü©
    dsimp
    simp only [Cochain.Œ¥_shift, Œ¥_snd, Cochain.shift_neg, smul_neg,
      Cochain.neg_v, Cochain.zsmul_v, Cochain.shift_v, Cochain.comp_zero_cochain_v,
      Cochain.ofHom_v, shiftFunctor_map_f', zsmul_comp, neg_add_self])
  inv := desc _ (Œµ n ‚Ä¢ (inl œÜ).shift n) ((inr œÜ)‚ü¶n‚üß') (by
    ext p
    dsimp
    simp only [Œ¥_zsmul, Cochain.Œ¥_shift, Œ¥_inl, Cochain.ofHom_comp, smul_smul,
      mul_Œµ_self, one_smul, Cochain.shift_v, Cochain.zero_cochain_comp_v, Cochain.ofHom_v,
      shiftFunctor_map_f'])
  hom_inv_id := by
    ext p
    dsimp
    simp only [lift_f _ _ _ _ _ _ rfl, desc_f _ _ _ _ _ _ rfl,
      Cocycle.coe_zsmul, Cocycle.shift_coe, Cochain.zsmul_v, Cochain.shift_v,
      shiftFunctor_map_f', comp_add, add_comp, assoc, inl_v_fst_v_assoc, inr_f_fst_v_assoc,
      zero_comp, comp_zero, add_zero, inl_v_snd_v_assoc, inr_f_snd_v_assoc, zero_add,
      comp_zsmul, zsmul_comp, smul_smul, mul_Œµ_self, one_smul, smul_zero]
    exact (id œÜ (p+n) (p+1+n) (by linarith)).symm
  inv_hom_id := by
    ext p
    dsimp
    simp only [lift_f _ _ _ _ _ _ rfl, desc_f _ _ _ _ _ _ rfl,
      Cochain.zsmul_v, Cochain.shift_v, comp_zsmul, shiftFunctor_map_f',
      Cocycle.coe_zsmul, Cocycle.shift_coe, zsmul_comp, comp_add, add_comp, assoc,
      inl_v_fst_v_assoc, inr_f_fst_v_assoc, zero_comp, comp_zero, add_zero, smul_smul,
      mul_Œµ_self, one_smul, inl_v_snd_v_assoc, smul_zero, inr_f_snd_v_assoc, zero_add]
    exact (id (œÜ‚ü¶n‚üß') p (p+1) (by linarith)).symm

set_option maxHeartbeats 400000 in
noncomputable def shiftTriangleIso (n : ‚Ñ§) :
    (Triangle.shiftFunctor _ n).obj (triangle œÜ) ‚âÖ triangle (œÜ‚ü¶n‚üß') :=
  Triangle.isoMk _ _ (Iso.refl _) (mulIso ((-1 : Units ‚Ñ§) ^ n) (Iso.refl _)) (shiftIso œÜ n)
    (by
      dsimp
      simp only [zsmul_comp, comp_zsmul, smul_smul, id_comp, comp_id]
      erw [mul_Œµ_self, one_smul])
    (by
      ext p
      dsimp [shiftIso]
      rw [lift_f _ _ _ _ p (p+1) rfl]
      simp only [Cocycle.coe_zsmul, Cocycle.shift_coe, Cochain.zsmul_v,
        Cochain.shift_v, zsmul_comp, comp_add, comp_zsmul, inr_f_fst_v_assoc, zero_comp,
        inr_f_snd_v_assoc, id_comp, smul_smul, mul_Œµ_self, one_smul, zero_add]
      rfl)
    (by
      ext p
      dsimp [shiftIso]
      rw [lift_f _ _ _ _ p (p+1) rfl]
      simp only [Cocycle.coe_zsmul, Cocycle.shift_coe, Cochain.zsmul_v, Cochain.shift_v,
        add_comp, assoc, inl_v_triangleŒ¥_f, shiftFunctor_obj_X, shiftFunctorObjXIso,
        HomologicalComplex.XIsoOfEq_rfl, Iso.refl_inv, comp_neg, comp_id, inr_f_triangleŒ¥_f,
        comp_zero, add_zero, zsmul_comp, shiftFunctorComm_hom_app_f]
      dsimp [triangleŒ¥]
      simp only [Cochain.rightShift_v _ 1 0 (zero_add 1) (p+n) (p+n) (add_zero _) (p+n+1) rfl,
        shiftFunctorObjXIso, assoc, neg_comp, smul_neg, neg_inj, Cochain.neg_v,
        HomologicalComplex.XIsoOfEq_rfl, Iso.refl_inv, comp_id,
        Cochain.v_comp_XIsoOfEq_hom_assoc]
      erw [comp_id])

end shift

end MappingCone

end CochainComplex

namespace HomotopyCategory

variable (C)

def distinguishedTriangles : Set (Triangle (HomotopyCategory C (ComplexShape.up ‚Ñ§))) :=
  fun T => ‚àÉ (X Y : CochainComplex C ‚Ñ§) (f : X ‚ü∂ Y),
    Nonempty (T ‚âÖ (HomotopyCategory.quotient C (ComplexShape.up ‚Ñ§)).mapTriangle.obj
      (CochainComplex.MappingCone.triangle f))

variable {C}

lemma isomorphic_distinguished (T‚ÇÅ : Triangle (HomotopyCategory C (ComplexShape.up ‚Ñ§)))
    (hT‚ÇÅ : T‚ÇÅ ‚àà distinguishedTriangles C) (T‚ÇÇ : Triangle (HomotopyCategory C (ComplexShape.up ‚Ñ§)))
    (e : T‚ÇÇ ‚âÖ T‚ÇÅ) : T‚ÇÇ ‚àà distinguishedTriangles C := by
  obtain ‚ü®X, Y, f, ‚ü®e'‚ü©‚ü© := hT‚ÇÅ
  exact ‚ü®X, Y, f, ‚ü®e ‚â™‚â´ e'‚ü©‚ü©

lemma contractible_distinguished (X : HomotopyCategory C (ComplexShape.up ‚Ñ§)) :
    contractibleTriangle X ‚àà distinguishedTriangles C := by
  obtain ‚ü®X‚ü© := X
  refine' ‚ü®_, _, ùüô X, ‚ü®_‚ü©‚ü©
  have h := (isZero_quotient_obj_iff _).2 ‚ü®CochainComplex.MappingCone.homotopyToZeroOfId X‚ü©
  exact Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) h.isoZero.symm
    (by simp) (h.eq_of_tgt _ _) (by dsimp ; ext)

lemma distinguished_cocone_triangle {X Y : HomotopyCategory C (ComplexShape.up ‚Ñ§)} (f : X ‚ü∂ Y) :
    ‚àÉ (Z : HomotopyCategory C (ComplexShape.up ‚Ñ§)) (g : Y ‚ü∂ Z) (h : Z ‚ü∂ X‚ü¶1‚üß),
      Triangle.mk f g h ‚àà distinguishedTriangles C := by
  obtain ‚ü®X‚ü© := X
  obtain ‚ü®Y‚ü© := Y
  obtain ‚ü®f, rfl‚ü© := (quotient _ _).map_surjective f
  exact ‚ü®_, _, _, ‚ü®_, _, f, ‚ü®Iso.refl _‚ü©‚ü©‚ü©

lemma complete_distinguished_triangle_morphism
    (T‚ÇÅ T‚ÇÇ : Triangle (HomotopyCategory C (ComplexShape.up ‚Ñ§)))
    (hT‚ÇÅ : T‚ÇÅ ‚àà distinguishedTriangles C) (hT‚ÇÇ : T‚ÇÇ ‚àà distinguishedTriangles C)
    (a : T‚ÇÅ.obj‚ÇÅ ‚ü∂ T‚ÇÇ.obj‚ÇÅ) (b : T‚ÇÅ.obj‚ÇÇ ‚ü∂ T‚ÇÇ.obj‚ÇÇ) (fac : T‚ÇÅ.mor‚ÇÅ ‚â´ b = a ‚â´ T‚ÇÇ.mor‚ÇÅ) :
    ‚àÉ (c : T‚ÇÅ.obj‚ÇÉ ‚ü∂ T‚ÇÇ.obj‚ÇÉ), T‚ÇÅ.mor‚ÇÇ ‚â´ c = b ‚â´ T‚ÇÇ.mor‚ÇÇ ‚àß
      T‚ÇÅ.mor‚ÇÉ ‚â´ a‚ü¶(1 : ‚Ñ§)‚üß' = c ‚â´ T‚ÇÇ.mor‚ÇÉ := by
  obtain ‚ü®K‚ÇÅ, L‚ÇÅ, œÜ‚ÇÅ, ‚ü®e‚ÇÅ‚ü©‚ü© := hT‚ÇÅ
  obtain ‚ü®K‚ÇÇ, L‚ÇÇ, œÜ‚ÇÇ, ‚ü®e‚ÇÇ‚ü©‚ü© := hT‚ÇÇ
  obtain ‚ü®a', ha'‚ü© : ‚àÉ (a' : (quotient _ _).obj K‚ÇÅ ‚ü∂ (quotient _ _).obj K‚ÇÇ),
    a' = e‚ÇÅ.inv.hom‚ÇÅ ‚â´ a ‚â´ e‚ÇÇ.hom.hom‚ÇÅ := ‚ü®_, rfl‚ü©
  obtain ‚ü®b', hb'‚ü© : ‚àÉ (b' : (quotient _ _).obj L‚ÇÅ ‚ü∂ (quotient _ _).obj L‚ÇÇ),
    b' = e‚ÇÅ.inv.hom‚ÇÇ ‚â´ b ‚â´ e‚ÇÇ.hom.hom‚ÇÇ := ‚ü®_, rfl‚ü©
  obtain ‚ü®a'', rfl‚ü© := (quotient _ _).map_surjective a'
  obtain ‚ü®b'', rfl‚ü© := (quotient _ _).map_surjective b'
  have H : Homotopy (œÜ‚ÇÅ ‚â´ b'') (a'' ‚â´ œÜ‚ÇÇ) := homotopyOfEq _ _ (by
    have comm‚ÇÅ‚ÇÅ := e‚ÇÅ.inv.comm‚ÇÅ
    have comm‚ÇÅ‚ÇÇ := e‚ÇÇ.hom.comm‚ÇÅ
    dsimp at comm‚ÇÅ‚ÇÅ comm‚ÇÅ‚ÇÇ
    simp only [Functor.map_comp, ha', hb', reassoc_of% comm‚ÇÅ‚ÇÅ,
      reassoc_of% fac, comm‚ÇÅ‚ÇÇ, assoc])
  let Œ≥ := e‚ÇÅ.hom ‚â´ CochainComplex.MappingCone.triangleMap H ‚â´ e‚ÇÇ.inv
  have comm‚ÇÇ := Œ≥.comm‚ÇÇ
  have comm‚ÇÉ := Œ≥.comm‚ÇÉ
  dsimp at comm‚ÇÇ comm‚ÇÉ
  simp only [ha', hb', assoc, Iso.hom_inv_id_triangle_hom‚ÇÅ_assoc,
    Iso.hom_inv_id_triangle_hom‚ÇÅ, Iso.hom_inv_id_triangle_hom‚ÇÇ_assoc, comp_id] at comm‚ÇÇ comm‚ÇÉ
  exact ‚ü®Œ≥.hom‚ÇÉ, comm‚ÇÇ, by dsimp ; simpa only [assoc] using comm‚ÇÉ‚ü©

lemma rotate_distinguished_triangle' (T : Triangle (HomotopyCategory C (ComplexShape.up ‚Ñ§)))
    (hT : T ‚àà distinguishedTriangles C) : T.rotate ‚àà distinguishedTriangles C := by
  obtain ‚ü®K, L, œÜ, ‚ü®e‚ü©‚ü© := hT
  let T‚ÇÄ := (quotient C (ComplexShape.up ‚Ñ§)).mapTriangle.obj
    (CochainComplex.MappingCone.triangle œÜ)
  suffices T‚ÇÄ.rotate ‚àà distinguishedTriangles C from
    isomorphic_distinguished _ this _ ((rotate _).mapIso e)
  refine' ‚ü®_, _ , CochainComplex.MappingCone.inr œÜ, ‚ü®_‚ü©‚ü©
  refine' Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _)
    (((quotient C (ComplexShape.up ‚Ñ§)).commShiftIso (1 : ‚Ñ§)).symm.app K ‚â™‚â´
      HomotopyCategory.isoOfHomotopyEquiv (CochainComplex.MappingCone.rotateHomotopyEquiv œÜ)) _ _ _
  . dsimp
    simp only [comp_id, id_comp]
  . dsimp
    simp only [assoc, Iso.hom_inv_id_app_assoc,
      CochainComplex.MappingCone.rotateHomotopyEquiv_comm‚ÇÇ, id_comp]
  . dsimp
    simp only [CategoryTheory.Functor.map_id, comp_id, assoc,
      CochainComplex.MappingCone.rotateHomotopyEquiv_comm‚ÇÉ'_assoc,
      neg_comp, comp_neg, neg_inj]
    erw [‚Üê NatTrans.naturality_assoc, Iso.inv_hom_id_app, comp_id]
    rfl

lemma shift_distinguished_triangle (T : Triangle (HomotopyCategory C (ComplexShape.up ‚Ñ§)))
    (hT : T ‚àà distinguishedTriangles C) (n : ‚Ñ§) :
      (Triangle.shiftFunctor _ n).obj T ‚àà distinguishedTriangles C := by
  obtain ‚ü®K, L, œÜ, ‚ü®e‚ü©‚ü© := hT
  let T‚ÇÄ := (quotient _ _).mapTriangle.obj (CochainComplex.MappingCone.triangle œÜ)
  suffices (Triangle.shiftFunctor _ n).obj T‚ÇÄ ‚àà distinguishedTriangles C from
    isomorphic_distinguished _ this _ ((Triangle.shiftFunctor _ n).mapIso e)
  exact ‚ü®_, _, œÜ‚ü¶n‚üß',
    ‚ü®((quotient C (ComplexShape.up ‚Ñ§)).mapTriangleCommShiftIso n).symm.app _ ‚â™‚â´
      (quotient _ _).mapTriangle.mapIso (CochainComplex.MappingCone.shiftTriangleIso œÜ n)‚ü©‚ü©

lemma invRotate_distinguished_triangle' (T : Triangle (HomotopyCategory C (ComplexShape.up ‚Ñ§)))
    (hT : T ‚àà distinguishedTriangles C) : T.invRotate ‚àà distinguishedTriangles C := by
  let e := (invRotateIsoRotateRotateShiftFunctorNegOne _).app T
  refine' isomorphic_distinguished  _ _ _ e
  apply shift_distinguished_triangle
  apply rotate_distinguished_triangle'
  apply rotate_distinguished_triangle'
  exact hT

lemma rotate_distinguished_triangle (T : Triangle (HomotopyCategory C (ComplexShape.up ‚Ñ§))) :
    T ‚àà distinguishedTriangles C ‚Üî T.rotate ‚àà distinguishedTriangles C := by
  constructor
  . exact rotate_distinguished_triangle' T
  . intro hT
    exact isomorphic_distinguished _ (invRotate_distinguished_triangle' T.rotate hT) _
      ((triangleRotation _).unitIso.app T)

instance : Pretriangulated (HomotopyCategory C (ComplexShape.up ‚Ñ§)) where
  distinguishedTriangles := distinguishedTriangles C
  isomorphic_distinguished := isomorphic_distinguished
  contractible_distinguished := contractible_distinguished
  distinguished_cocone_triangle := distinguished_cocone_triangle
  rotate_distinguished_triangle := rotate_distinguished_triangle
  complete_distinguished_triangle_morphism := complete_distinguished_triangle_morphism

lemma mappingCone_triangle_distinguished {X Y : CochainComplex C ‚Ñ§} (f : X ‚ü∂ Y) :
  (HomotopyCategory.quotient C (ComplexShape.up ‚Ñ§)).mapTriangle.obj
      (CochainComplex.MappingCone.triangle f) ‚àà distTriang (HomotopyCategory _ _) :=
  ‚ü®_, _, f, ‚ü®Iso.refl _‚ü©‚ü©

end HomotopyCategory
