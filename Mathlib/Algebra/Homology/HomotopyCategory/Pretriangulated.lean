/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.HomotopyCategory.MappingCone
import Mathlib.Algebra.Homology.HomotopyCategory.HomComplexShift
import Mathlib.CategoryTheory.Triangulated.Functor

/-! The pretriangulated structure on the homotopy category of complexes

In this file, we define the pretriangulated structure on the homotopy
category `HomotopyCategory C (ComplexShape.up â„¤)` of an additive category `C`.
The distinguished triangles are the triangles that are isomorphic to the
image in the homotopy category of the standard triangle
`K âŸ¶ L âŸ¶ mappingCone Ï† âŸ¶ KâŸ¦(1 : â„¤)âŸ§` for some morphism of
cochain complexes `Ï† : K âŸ¶ L`.

This result first appeared in the Liquid Tensor Experiment. In the LTE, the
formalization followed the Stacks Project: in particular, the distinguished
triangles were defined using degreewise-split short exact sequences of cochain
complexes. Here, we follow the original definitions in [Verdiers's thesis, I.3][verdier1996]
(with the better sign conventions from the introduction of
[Brian Conrad's book *Grothendieck duality and base change*][conrad2000]).

## References
* [Jean-Louis Verdier, *Des catÃ©gories dÃ©rivÃ©es des catÃ©gories abÃ©liennes*][verdier1996]
* [Brian Conrad, Grothendieck duality and base change][conrad2000]
* https://stacks.math.columbia.edu/tag/014P

-/

open CategoryTheory Category Limits CochainComplex.HomComplex Pretriangulated

variable {C : Type*} [Category C] [Preadditive C] [HasZeroObject C] [HasBinaryBiproducts C]
  {K L : CochainComplex C â„¤} (Ï† : K âŸ¶ L)

namespace CochainComplex

namespace mappingCone

/-- The standard triangle `K âŸ¶ L âŸ¶ mappingCone Ï† âŸ¶ KâŸ¦(1 : â„¤)âŸ§` in `CochainComplex C â„¤`
attached to a morphism `Ï† : K âŸ¶ L`. It involves `Ï†`, `inr Ï† : L âŸ¶ mappingCone Ï†` and
the morphism induced by the `1`-cocycle `-mappingCone.fst Ï†`. -/
@[simps! objâ‚ objâ‚‚ objâ‚ƒ morâ‚ morâ‚‚]
noncomputable def triangle : Triangle (CochainComplex C â„¤) :=
  Triangle.mk Ï† (inr Ï†) (Cocycle.homOf ((-fst Ï†).rightShift 1 0 (zero_add 1)))

@[reassoc (attr := simp)]
lemma inl_v_triangle_morâ‚ƒ_f (p q : â„¤) (hpq : p + (-1) = q) :
    (inl Ï†).v p q hpq â‰« (triangle Ï†).morâ‚ƒ.f q =
      -(K.shiftFunctorObjXIso 1 q p (by rw [â† hpq, neg_add_cancel_right])).inv := by
  simp [triangle, Cochain.rightShift_v _ 1 0 (zero_add 1) q q (add_zero q) p (by omega)]

@[reassoc (attr := simp)]
lemma inr_f_triangle_morâ‚ƒ_f (p : â„¤) : (inr Ï†).f p â‰« (triangle Ï†).morâ‚ƒ.f p = 0 := by
  simp [triangle, Cochain.rightShift_v _ 1 0 _ p p (add_zero p) (p+1) rfl]

@[reassoc (attr := simp)]
lemma inr_triangleÎ´ : inr Ï† â‰« (triangle Ï†).morâ‚ƒ = 0 := by aesop_cat

/-- The (distinguished) triangle in the homotopy category that is associated to
a morphism `Ï† : K âŸ¶ L` in the category `CochainComplex C â„¤`. -/
noncomputable abbrev triangleh : Triangle (HomotopyCategory C (ComplexShape.up â„¤)) :=
  (HomotopyCategory.quotient _ _).mapTriangle.obj (triangle Ï†)

variable (K)

/-- The mapping cone of the identity is contractible. -/
noncomputable def homotopyToZeroOfId : Homotopy (ğŸ™ (mappingCone (ğŸ™ K))) 0 :=
  descHomotopy (ğŸ™ K) _ _ 0 (inl _) (by simp) (by simp)

variable {K}

section mapOfHomotopy

variable {Kâ‚ Lâ‚ Kâ‚‚ Lâ‚‚ Kâ‚ƒ Lâ‚ƒ : CochainComplex C â„¤} {Ï†â‚ : Kâ‚ âŸ¶ Lâ‚} {Ï†â‚‚ : Kâ‚‚ âŸ¶ Lâ‚‚}
  {a : Kâ‚ âŸ¶ Kâ‚‚} {b : Lâ‚ âŸ¶ Lâ‚‚} (H : Homotopy (Ï†â‚ â‰« b) (a â‰« Ï†â‚‚))

/-- The morphism `mappingCone Ï†â‚ âŸ¶ mappingCone Ï†â‚‚` that is induced by a square that
is commutative up to homotopy. -/
noncomputable def mapOfHomotopy :
    mappingCone Ï†â‚ âŸ¶ mappingCone Ï†â‚‚ :=
  desc Ï†â‚ ((Cochain.ofHom a).comp (inl Ï†â‚‚) (zero_add _) +
    ((Cochain.equivHomotopy _ _) H).1.comp (Cochain.ofHom (inr Ï†â‚‚)) (add_zero _))
    (b â‰« inr Ï†â‚‚) (by simp)

@[reassoc]
lemma triangleMapOfHomotopy_commâ‚‚ :
    inr Ï†â‚ â‰« mapOfHomotopy H = b â‰« inr Ï†â‚‚ := by
  simp [mapOfHomotopy]

@[reassoc]
lemma triangleMapOfHomotopy_commâ‚ƒ :
    mapOfHomotopy H â‰« (triangle Ï†â‚‚).morâ‚ƒ = (triangle Ï†â‚).morâ‚ƒ â‰« aâŸ¦1âŸ§' := by
  ext p
  simp [ext_from_iff _ _ _ rfl, triangle, mapOfHomotopy,
    Cochain.rightShift_v _ 1 0 _ p p _ (p + 1) rfl]

/-- The morphism `triangleh Ï†â‚ âŸ¶ triangleh Ï†â‚‚` that is induced by a square that
is commutative up to homotopy. -/
@[simps]
noncomputable def trianglehMapOfHomotopy :
    triangleh Ï†â‚ âŸ¶ triangleh Ï†â‚‚ where
  homâ‚ := (HomotopyCategory.quotient _ _).map a
  homâ‚‚ := (HomotopyCategory.quotient _ _).map b
  homâ‚ƒ := (HomotopyCategory.quotient _ _).map (mapOfHomotopy H)
  commâ‚ := by
    dsimp
    simp only [â† Functor.map_comp]
    exact HomotopyCategory.eq_of_homotopy _ _ H
  commâ‚‚ := by
    dsimp
    simp only [â† Functor.map_comp, triangleMapOfHomotopy_commâ‚‚]
  commâ‚ƒ := by
    dsimp
    rw [â† Functor.map_comp_assoc, triangleMapOfHomotopy_commâ‚ƒ, Functor.map_comp, assoc, assoc]
    erw [â† NatTrans.naturality]
    rfl

end mapOfHomotopy

section map

variable {Kâ‚ Lâ‚ Kâ‚‚ Lâ‚‚ Kâ‚ƒ Lâ‚ƒ : CochainComplex C â„¤} (Ï†â‚ : Kâ‚ âŸ¶ Lâ‚) (Ï†â‚‚ : Kâ‚‚ âŸ¶ Lâ‚‚) (Ï†â‚ƒ : Kâ‚ƒ âŸ¶ Lâ‚ƒ)
  (a : Kâ‚ âŸ¶ Kâ‚‚) (b : Lâ‚ âŸ¶ Lâ‚‚) (comm : Ï†â‚ â‰« b = a â‰« Ï†â‚‚)
  (a' : Kâ‚‚ âŸ¶ Kâ‚ƒ) (b' : Lâ‚‚ âŸ¶ Lâ‚ƒ) (comm' : Ï†â‚‚ â‰« b' = a' â‰« Ï†â‚ƒ)

/-- The morphism `mappingCone Ï†â‚ âŸ¶ mappingCone Ï†â‚‚` that is induced by a commutative square. -/
noncomputable def map : mappingCone Ï†â‚ âŸ¶ mappingCone Ï†â‚‚ :=
  desc Ï†â‚ ((Cochain.ofHom a).comp (inl Ï†â‚‚) (zero_add _)) (b â‰« inr Ï†â‚‚)
    (by simp [reassoc_of% comm])

lemma map_eq_mapOfHomotopy : map Ï†â‚ Ï†â‚‚ a b comm = mapOfHomotopy (Homotopy.ofEq comm) := by
  simp [map, mapOfHomotopy]

lemma map_id : map Ï† Ï† (ğŸ™ _) (ğŸ™ _) (by rw [id_comp, comp_id]) = ğŸ™ _ := by
  ext n
  simp [ext_from_iff _ (n + 1) n rfl, map]

@[reassoc]
lemma map_comp : map Ï†â‚ Ï†â‚ƒ (a â‰« a') (b â‰« b') (by rw [reassoc_of% comm, comm', assoc]) =
    map Ï†â‚ Ï†â‚‚ a b comm â‰« map Ï†â‚‚ Ï†â‚ƒ a' b' comm' := by
  ext n
  simp [ext_from_iff _ (n+1) n rfl, map]

/-- The morphism `triangle Ï†â‚ âŸ¶ triangle Ï†â‚‚` that is induced by a commutative square. -/
@[simps]
noncomputable def triangleMap :
    triangle Ï†â‚ âŸ¶ triangle Ï†â‚‚ where
  homâ‚ := a
  homâ‚‚ := b
  homâ‚ƒ := map Ï†â‚ Ï†â‚‚ a b comm
  commâ‚ := comm
  commâ‚‚ := by
    dsimp
    rw [map_eq_mapOfHomotopy, triangleMapOfHomotopy_commâ‚‚]
  commâ‚ƒ := by
    dsimp
    rw [map_eq_mapOfHomotopy, triangleMapOfHomotopy_commâ‚ƒ]

end map

/-

As the number of simp lemmas that are required in the proofs below is huge,
we use the `simp? ... says` syntax: it is meant to ease the maintenance
of this code as it gives a minimal list of lemmas with which `simp`
is able to finish the proof.

The `set_option maxHeartbeats` are necessary only when this code is compiled
with `set_option says.verify true` (e.g. during CI).

-/

section Rotate

set_option maxHeartbeats 400000 in
/-- Given `Ï† : K âŸ¶ L`, `KâŸ¦(1 : â„¤)âŸ§` is homotopy equivalent to
the mapping cone of `inr Ï† : L âŸ¶ mappingCone Ï†`. -/
noncomputable def rotateHomotopyEquiv :
    HomotopyEquiv (KâŸ¦(1 : â„¤)âŸ§) (mappingCone (inr Ï†)) where
  hom := lift (inr Ï†) (-(Cocycle.ofHom Ï†).leftShift 1 1 (zero_add 1))
    (-(inl Ï†).leftShift 1 0 (neg_add_self 1)) (by
      simp? [Cochain.Î´_leftShift _ 1 0 1 (neg_add_self 1) 0 (zero_add 1)] says
        simp only [Î´_neg, Cochain.Î´_leftShift _ 1 0 1 (neg_add_self 1) 0 (zero_add 1),
          Int.negOnePow_one, Î´_inl, Cochain.ofHom_comp, Cochain.leftShift_comp_zero_cochain,
          Units.neg_smul, one_smul, neg_neg, Cocycle.coe_neg, Cocycle.leftShift_coe,
          Cocycle.ofHom_coe, Cochain.neg_comp, add_right_neg])
  inv := desc (inr Ï†) 0 (triangle Ï†).morâ‚ƒ
    (by simp only [Î´_zero, inr_triangleÎ´, Cochain.ofHom_zero])
  homotopyHomInvId := Homotopy.ofEq <| by
    ext n
    simp? [lift_desc_f _ _ _ _ _ _ _ _ _ rfl,
        (inl Ï†).leftShift_v 1 0 _ _ n _ (n + 1)
          (by
            simp only [add_neg_cancel_right])] says simp only [shiftFunctor_obj_X',
        HomologicalComplex.comp_f, lift_desc_f _ _ _ _ _ _ _ _ _ rfl, Cocycle.coe_neg,
        Cocycle.leftShift_coe, Cocycle.ofHom_coe, Cochain.neg_v, Cochain.zero_v, comp_zero,
        (inl Ï†).leftShift_v 1 0 _ _ n _ (n + 1) (by simp only [add_neg_cancel_right]),
        shiftFunctor_obj_X, â†“mul_zero, sub_self, one_mul, Int.zero_ediv, add_zero,
        Int.negOnePow_zero, shiftFunctorObjXIso, HomologicalComplex.XIsoOfEq_rfl, Iso.refl_hom,
        id_comp, one_smul, Preadditive.neg_comp, inl_v_triangle_morâ‚ƒ_f, Iso.refl_inv, neg_neg,
        zero_add, HomologicalComplex.id_f]
  homotopyInvHomId := (Cochain.equivHomotopy _ _).symm
    âŸ¨-(snd (inr Ï†)).comp ((snd Ï†).comp (inl (inr Ï†)) (zero_add (-1))) (zero_add (-1)), by
      ext n
      simp? [ext_to_iff _ _ (n + 1) rfl, ext_from_iff _ (n + 1) _ rfl,
          Î´_zero_cochain_comp _ _ _ (neg_add_self 1),
          (inl Ï†).leftShift_v 1 0 (neg_add_self 1) n n (add_zero n) (n + 1) (by omega),
          (Cochain.ofHom Ï†).leftShift_v 1 1 (zero_add 1) n (n + 1) rfl (n + 1) (by omega),
          Cochain.comp_v _ _ (add_neg_self 1) n (n + 1) n rfl
            (by
              omega)] says simp only [Cochain.ofHom_comp, ofHom_desc, ofHom_lift, Cocycle.coe_neg,
          Cocycle.leftShift_coe, Cocycle.ofHom_coe, Cochain.comp_zero_cochain_v,
          shiftFunctor_obj_X', Î´_neg, Î´_zero_cochain_comp _ _ _ (neg_add_self 1), Î´_inl,
          Int.reduceNeg, Int.negOnePow_neg, Int.negOnePow_one, Î´_snd, Cochain.neg_comp,
          Cochain.comp_assoc_of_second_is_zero_cochain, smul_neg, Units.neg_smul, one_smul, neg_neg,
          Cochain.comp_add, inr_snd_assoc, neg_add_rev, Cochain.add_v, Cochain.neg_v,
          Cochain.comp_v _ _ (add_neg_self 1) n (n + 1) n rfl (by omega),
          Cochain.zero_cochain_comp_v, Cochain.ofHom_v, HomologicalComplex.id_f,
          ext_to_iff _ _ (n + 1) rfl, assoc, liftCochain_v_fst_v,
          (Cochain.ofHom Ï†).leftShift_v 1 1 (zero_add 1) n (n + 1) rfl (n + 1) (by omega),
          shiftFunctor_obj_X, mul_one, sub_self, one_mul, Int.zero_ediv, add_zero,
          shiftFunctorObjXIso, HomologicalComplex.XIsoOfEq_rfl, Iso.refl_hom, id_comp,
          Preadditive.add_comp, Preadditive.neg_comp, inl_v_fst_v, comp_id, inr_f_fst_v, comp_zero,
          neg_zero, neg_add_cancel_comm, ext_from_iff _ (n + 1) _ rfl, inl_v_descCochain_v_assoc,
          Cochain.zero_v, zero_comp, Preadditive.comp_neg, inl_v_snd_v_assoc,
          inr_f_descCochain_v_assoc, inr_f_snd_v_assoc, inl_v_triangle_morâ‚ƒ_f_assoc, triangle_objâ‚,
          Iso.refl_inv, inl_v_fst_v_assoc, inr_f_triangle_morâ‚ƒ_f_assoc, inr_f_fst_v_assoc, and_self,
          liftCochain_v_snd_v,
          (inl Ï†).leftShift_v 1 0 (neg_add_self 1) n n (add_zero n) (n + 1) (by omega), â†“mul_zero,
          Int.negOnePow_zero, inl_v_snd_v, inr_f_snd_v, zero_add, inl_v_descCochain_v,
          inr_f_descCochain_v, inl_v_triangle_morâ‚ƒ_f, inr_f_triangle_morâ‚ƒ_f, add_left_neg]âŸ©

/-- Auxiliary definition for `rotateTrianglehIso`. -/
noncomputable def rotateHomotopyEquivCommâ‚‚Homotopy :
  Homotopy ((triangle Ï†).morâ‚ƒ â‰« (rotateHomotopyEquiv Ï†).hom)
    (inr (CochainComplex.mappingCone.inr Ï†)) := (Cochain.equivHomotopy _ _).symm
      âŸ¨-(snd Ï†).comp (inl (inr Ï†)) (zero_add (-1)), by
        ext p
        dsimp [rotateHomotopyEquiv]
        simp [ext_from_iff _ _ _ rfl, ext_to_iff _ _ _ rfl,
          (inl Ï†).leftShift_v 1 0 (neg_add_self 1) p p (add_zero p) (p + 1) (by omega),
          Î´_zero_cochain_comp _ _ _ (neg_add_self 1),
          Cochain.comp_v _ _ (add_neg_self 1) p (p + 1) p rfl (by omega),
          (Cochain.ofHom Ï†).leftShift_v 1 1 (zero_add 1) p (p + 1) rfl (p + 1) (by omega)]âŸ©

@[reassoc (attr := simp)]
lemma rotateHomotopyEquiv_commâ‚‚ :
    (HomotopyCategory.quotient _ _ ).map (triangle Ï†).morâ‚ƒ â‰«
      (HomotopyCategory.quotient _ _ ).map (rotateHomotopyEquiv Ï†).hom =
      (HomotopyCategory.quotient _ _ ).map (inr (inr Ï†)) := by
  simpa only [Functor.map_comp]
    using HomotopyCategory.eq_of_homotopy _ _  (rotateHomotopyEquivCommâ‚‚Homotopy Ï†)

@[reassoc (attr := simp)]
lemma rotateHomotopyEquiv_commâ‚ƒ :
    (rotateHomotopyEquiv Ï†).hom â‰« (triangle (inr Ï†)).morâ‚ƒ = -Ï†âŸ¦1âŸ§' := by
  ext p
  dsimp [rotateHomotopyEquiv]
  simp [lift_f _ _ _ _ _ (p + 1) rfl,
    (Cochain.ofHom Ï†).leftShift_v 1 1 (zero_add 1) p (p + 1) rfl (p + 1) (by omega)]

/-- The canonical isomorphism of triangles `(triangleh Ï†).rotate â‰… (triangleh (inr Ï†))`. -/
noncomputable def rotateTrianglehIso :
    (triangleh Ï†).rotate â‰… (triangleh (inr Ï†)) :=
  Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _)
    (((HomotopyCategory.quotient C (ComplexShape.up â„¤)).commShiftIso (1 : â„¤)).symm.app K â‰ªâ‰«
      HomotopyCategory.isoOfHomotopyEquiv (rotateHomotopyEquiv Ï†)) (by aesop_cat) (by aesop_cat) (by
        dsimp
        rw [CategoryTheory.Functor.map_id, comp_id, assoc, â† Functor.map_comp_assoc,
          rotateHomotopyEquiv_commâ‚ƒ, Functor.map_neg, Preadditive.neg_comp,
          Functor.commShiftIso_hom_naturality, Preadditive.comp_neg,
          Iso.inv_hom_id_app_assoc])

end Rotate

section Shift

set_option maxHeartbeats 800000 in
/-- The canonical isomorphism `(mappingCone Ï†)âŸ¦nâŸ§ â‰… mappingCone (Ï†âŸ¦nâŸ§')`. -/
noncomputable def shiftIso (n : â„¤) : (mappingCone Ï†)âŸ¦nâŸ§ â‰… mappingCone (Ï†âŸ¦nâŸ§') where
  hom := lift _ (n.negOnePow â€¢ (fst Ï†).shift n) ((snd Ï†).shift n) (by
    ext p q hpq
    dsimp
    simp? [Cochain.shift_v'] says
      simp only [Cochain.Î´_shift, Î´_snd, Cochain.shift_neg, smul_neg, Cochain.neg_v,
        shiftFunctor_obj_X', Cochain.units_smul_v, Cochain.shift_v', Cochain.comp_zero_cochain_v,
        Cochain.ofHom_v, Cochain.units_smul_comp, shiftFunctor_map_f', add_left_neg])
  inv := desc _ (n.negOnePow â€¢ (inl Ï†).shift n) ((inr Ï†)âŸ¦nâŸ§') (by
    ext p
    dsimp
    simp? [Cochain.shift_v', smul_smul] says
      simp only [Î´_units_smul, Cochain.Î´_shift, Î´_inl, Cochain.ofHom_comp, smul_smul,
        Int.units_mul_self, one_smul, Cochain.shift_v', Cochain.comp_zero_cochain_v,
        Cochain.ofHom_v, shiftFunctor_obj_X', shiftFunctor_map_f'])
  hom_inv_id := by
    ext p
    dsimp
    simp? [lift_desc_f _ _ _ _ _ _ _ _ (p + 1) rfl, id_X, smul_smul, Cochain.shift_v'] says
      simp only [lift_desc_f _ _ _ _ _ _ _ _ (p + 1) rfl, shiftFunctor_obj_X',
        Cocycle.coe_units_smul, Cocycle.shift_coe, Cochain.units_smul_v, Cochain.shift_v',
        Linear.comp_units_smul, Linear.units_smul_comp, smul_smul, Int.units_mul_self, one_smul,
        shiftFunctor_map_f', id_X]
  inv_hom_id := by
    ext p
    dsimp
    simp? [ext_from_iff _ (p + 1) _ rfl, ext_to_iff _ _ (p + 1) rfl,
        Cochain.shift_v', smul_smul] says
      simp only [ext_from_iff _ (p + 1) _ rfl, shiftFunctor_obj_X', inl_v_desc_f_assoc,
        Cochain.units_smul_v, Cochain.shift_v', Linear.units_smul_comp, comp_id,
        ext_to_iff _ _ (p + 1) rfl, assoc, lift_f_fst_v,
        Cocycle.coe_units_smul, Cocycle.shift_coe, Linear.comp_units_smul, inl_v_fst_v, smul_smul,
        Int.units_mul_self, one_smul, lift_f_snd_v, inl_v_snd_v, smul_zero, and_self,
        inr_f_desc_f_assoc, shiftFunctor_map_f', inr_f_fst_v, inr_f_snd_v]

set_option maxHeartbeats 800000 in
/-- The canonical isomorphism `(triangle Ï†)âŸ¦nâŸ§ â‰… triangle (Ï†âŸ¦nâŸ§')`. -/
noncomputable def shiftTriangleIso (n : â„¤) :
    (Triangle.shiftFunctor _ n).obj (triangle Ï†) â‰… triangle (Ï†âŸ¦nâŸ§') := by
  refine Triangle.isoMk _ _ (Iso.refl _) (n.negOnePow â€¢ Iso.refl _) (shiftIso Ï† n) ?_ ?_ ?_
  Â· simp? [Units.smul_def, smul_smul] says
      simp only [Triangle.shiftFunctor_obj, triangle_objâ‚, triangle_objâ‚‚, triangle_objâ‚ƒ,
        triangle_morâ‚, Units.smul_def, triangle_morâ‚‚, Functor.comp_obj, Triangle.mk_objâ‚,
        Triangle.mk_objâ‚‚, Triangle.mk_morâ‚, Preadditive.smul_iso_hom, Iso.refl_hom,
        Linear.comp_smul, comp_id, smul_smul, Int.units_coe_mul_self, one_smul, id_comp]
  Â· ext p
    set_option tactic.skipAssignedInstances false in
    dsimp
    simp? [shiftIso, Units.smul_def, ext_to_iff _ _ (p + 1) rfl, Cochain.shift_v'] says
      simp only [Units.smul_def, shiftIso, Linear.smul_comp, id_comp,
        ext_to_iff _ _ (p + 1) rfl, shiftFunctor_obj_X', assoc, lift_f_fst_v, Cocycle.coe_smul,
        Cocycle.shift_coe, Cochain.smul_v, Cochain.shift_v', Linear.comp_smul, inr_f_fst_v,
        smul_zero, lift_f_snd_v, inr_f_snd_v, and_true]
    rw [smul_zero]
  Â· ext p
    dsimp
    simp? [shiftIso, Units.smul_def, shiftFunctorComm_hom_app_f, triangle, Cochain.shift_v',
      (fst (Ï†âŸ¦nâŸ§')).1.rightShift_v 1 0 (zero_add 1) p p (add_zero p) (p + 1) rfl,
      (fst Ï†).1.rightShift_v 1 0 (zero_add 1) (p + n) (p + n)
        (add_zero (p + n)) (p + 1 + n) (by omega)] says
      simp only [triangle, Triangle.mk_morâ‚ƒ, Cocycle.homOf_f, Cocycle.rightShift_coe,
        Cocycle.coe_neg, Cochain.rightShift_neg, Cochain.neg_v, shiftFunctor_obj_X',
        (fst Ï†).1.rightShift_v 1 0 (zero_add 1) (p + n) (p + n) (add_zero (p + n)) (p + 1 + n)
          (by omega),
        shiftFunctor_obj_X, shiftFunctorObjXIso, shiftFunctorComm_hom_app_f, Preadditive.neg_comp,
        assoc, Iso.inv_hom_id, comp_id, smul_neg, Units.smul_def, shiftIso,
        (fst (Ï†âŸ¦nâŸ§')).1.rightShift_v 1 0 (zero_add 1) p p (add_zero p) (p + 1) rfl,
        HomologicalComplex.XIsoOfEq_rfl, Iso.refl_inv, Preadditive.comp_neg, lift_f_fst_v,
        Cocycle.coe_smul, Cocycle.shift_coe, Cochain.smul_v, Cochain.shift_v']

/-- The canonical isomorphism `(triangleh Ï†)âŸ¦nâŸ§ â‰… triangleh (Ï†âŸ¦nâŸ§')`. -/
noncomputable def shiftTrianglehIso (n : â„¤) :
    (Triangle.shiftFunctor _ n).obj (triangleh Ï†) â‰… triangleh (Ï†âŸ¦nâŸ§') :=
  ((HomotopyCategory.quotient _ _).mapTriangle.commShiftIso n).symm.app _ â‰ªâ‰«
    (HomotopyCategory.quotient _ _).mapTriangle.mapIso (shiftTriangleIso Ï† n)

end Shift

end mappingCone

end CochainComplex

namespace HomotopyCategory

variable (C)

namespace Pretriangulated

/-- A triangle in `HomotopyCategory C (ComplexShape.up â„¤)` is distinguished if it is isomorphic to
the triangle `CochainComplex.mappingCone.triangleh Ï†` for some morphism of cochain
complexes `Ï†`. -/
def distinguishedTriangles : Set (Triangle (HomotopyCategory C (ComplexShape.up â„¤))) :=
  fun T => âˆƒ (X Y : CochainComplex C â„¤) (Ï† : X âŸ¶ Y),
    Nonempty (T â‰… CochainComplex.mappingCone.triangleh Ï†)

variable {C}

lemma isomorphic_distinguished (Tâ‚ : Triangle (HomotopyCategory C (ComplexShape.up â„¤)))
    (hTâ‚ : Tâ‚ âˆˆ distinguishedTriangles C) (Tâ‚‚ : Triangle (HomotopyCategory C (ComplexShape.up â„¤)))
    (e : Tâ‚‚ â‰… Tâ‚) : Tâ‚‚ âˆˆ distinguishedTriangles C := by
  obtain âŸ¨X, Y, f, âŸ¨e'âŸ©âŸ© := hTâ‚
  exact âŸ¨X, Y, f, âŸ¨e â‰ªâ‰« e'âŸ©âŸ©

lemma contractible_distinguished (X : HomotopyCategory C (ComplexShape.up â„¤)) :
    Pretriangulated.contractibleTriangle X âˆˆ distinguishedTriangles C := by
  obtain âŸ¨XâŸ© := X
  refine' âŸ¨_, _, ğŸ™ X, âŸ¨_âŸ©âŸ©
  have h := (isZero_quotient_obj_iff _).2 âŸ¨CochainComplex.mappingCone.homotopyToZeroOfId XâŸ©
  exact Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) h.isoZero.symm
    (by simp) (h.eq_of_tgt _ _) (by dsimp; ext)

lemma distinguished_cocone_triangle {X Y : HomotopyCategory C (ComplexShape.up â„¤)} (f : X âŸ¶ Y) :
    âˆƒ (Z : HomotopyCategory C (ComplexShape.up â„¤)) (g : Y âŸ¶ Z) (h : Z âŸ¶ XâŸ¦1âŸ§),
      Triangle.mk f g h âˆˆ distinguishedTriangles C := by
  obtain âŸ¨XâŸ© := X
  obtain âŸ¨YâŸ© := Y
  obtain âŸ¨f, rflâŸ© := (quotient _ _).map_surjective f
  exact âŸ¨_, _, _, âŸ¨_, _, f, âŸ¨Iso.refl _âŸ©âŸ©âŸ©

lemma rotate_distinguished_triangle' (T : Triangle (HomotopyCategory C (ComplexShape.up â„¤)))
    (hT : T âˆˆ distinguishedTriangles C) : T.rotate âˆˆ distinguishedTriangles C := by
  obtain âŸ¨K, L, Ï†, âŸ¨eâŸ©âŸ© := hT
  exact âŸ¨_, _, _, âŸ¨(rotate _).mapIso e â‰ªâ‰« CochainComplex.mappingCone.rotateTrianglehIso Ï†âŸ©âŸ©

lemma shift_distinguished_triangle (T : Triangle (HomotopyCategory C (ComplexShape.up â„¤)))
    (hT : T âˆˆ distinguishedTriangles C) (n : â„¤) :
      (Triangle.shiftFunctor _ n).obj T âˆˆ distinguishedTriangles C := by
  obtain âŸ¨K, L, Ï†, âŸ¨eâŸ©âŸ© := hT
  exact âŸ¨_, _, _, âŸ¨Functor.mapIso _ e â‰ªâ‰« CochainComplex.mappingCone.shiftTrianglehIso Ï† nâŸ©âŸ©

lemma invRotate_distinguished_triangle' (T : Triangle (HomotopyCategory C (ComplexShape.up â„¤)))
    (hT : T âˆˆ distinguishedTriangles C) : T.invRotate âˆˆ distinguishedTriangles C :=
  isomorphic_distinguished _
    (shift_distinguished_triangle _ (rotate_distinguished_triangle' _
      (rotate_distinguished_triangle' _ hT)) _) _
    ((invRotateIsoRotateRotateShiftFunctorNegOne _).app T)

lemma rotate_distinguished_triangle (T : Triangle (HomotopyCategory C (ComplexShape.up â„¤))) :
    T âˆˆ distinguishedTriangles C â†” T.rotate âˆˆ distinguishedTriangles C := by
  constructor
  Â· exact rotate_distinguished_triangle' T
  Â· intro hT
    exact isomorphic_distinguished _ (invRotate_distinguished_triangle' T.rotate hT) _
      ((triangleRotation _).unitIso.app T)

lemma complete_distinguished_triangle_morphism
    (Tâ‚ Tâ‚‚ : Triangle (HomotopyCategory C (ComplexShape.up â„¤)))
    (hTâ‚ : Tâ‚ âˆˆ distinguishedTriangles C) (hTâ‚‚ : Tâ‚‚ âˆˆ distinguishedTriangles C)
    (a : Tâ‚.objâ‚ âŸ¶ Tâ‚‚.objâ‚) (b : Tâ‚.objâ‚‚ âŸ¶ Tâ‚‚.objâ‚‚) (fac : Tâ‚.morâ‚ â‰« b = a â‰« Tâ‚‚.morâ‚) :
    âˆƒ (c : Tâ‚.objâ‚ƒ âŸ¶ Tâ‚‚.objâ‚ƒ), Tâ‚.morâ‚‚ â‰« c = b â‰« Tâ‚‚.morâ‚‚ âˆ§
      Tâ‚.morâ‚ƒ â‰« aâŸ¦(1 : â„¤)âŸ§' = c â‰« Tâ‚‚.morâ‚ƒ := by
  obtain âŸ¨Kâ‚, Lâ‚, Ï†â‚, âŸ¨eâ‚âŸ©âŸ© := hTâ‚
  obtain âŸ¨Kâ‚‚, Lâ‚‚, Ï†â‚‚, âŸ¨eâ‚‚âŸ©âŸ© := hTâ‚‚
  obtain âŸ¨a', ha'âŸ© : âˆƒ (a' : (quotient _ _).obj Kâ‚ âŸ¶ (quotient _ _).obj Kâ‚‚),
    a' = eâ‚.inv.homâ‚ â‰« a â‰« eâ‚‚.hom.homâ‚ := âŸ¨_, rflâŸ©
  obtain âŸ¨b', hb'âŸ© : âˆƒ (b' : (quotient _ _).obj Lâ‚ âŸ¶ (quotient _ _).obj Lâ‚‚),
    b' = eâ‚.inv.homâ‚‚ â‰« b â‰« eâ‚‚.hom.homâ‚‚ := âŸ¨_, rflâŸ©
  obtain âŸ¨a'', rflâŸ© := (quotient _ _).map_surjective a'
  obtain âŸ¨b'', rflâŸ© := (quotient _ _).map_surjective b'
  have H : Homotopy (Ï†â‚ â‰« b'') (a'' â‰« Ï†â‚‚) := homotopyOfEq _ _ (by
    have commâ‚â‚ := eâ‚.inv.commâ‚
    have commâ‚â‚‚ := eâ‚‚.hom.commâ‚
    dsimp at commâ‚â‚ commâ‚â‚‚
    simp only [Functor.map_comp, ha', hb', reassoc_of% commâ‚â‚,
      reassoc_of% fac, commâ‚â‚‚, assoc])
  let Î³ := eâ‚.hom â‰« CochainComplex.mappingCone.trianglehMapOfHomotopy H â‰« eâ‚‚.inv
  have commâ‚‚ := Î³.commâ‚‚
  have commâ‚ƒ := Î³.commâ‚ƒ
  dsimp [Î³] at commâ‚‚ commâ‚ƒ
  simp only [ha', hb'] at commâ‚‚ commâ‚ƒ
  exact âŸ¨Î³.homâ‚ƒ, by simpa [Î³] using commâ‚‚, by simpa [Î³] using commâ‚ƒâŸ©

end Pretriangulated

instance : Pretriangulated (HomotopyCategory C (ComplexShape.up â„¤)) where
  distinguishedTriangles := Pretriangulated.distinguishedTriangles C
  isomorphic_distinguished := Pretriangulated.isomorphic_distinguished
  contractible_distinguished := Pretriangulated.contractible_distinguished
  distinguished_cocone_triangle := Pretriangulated.distinguished_cocone_triangle
  rotate_distinguished_triangle := Pretriangulated.rotate_distinguished_triangle
  complete_distinguished_triangle_morphism :=
    Pretriangulated.complete_distinguished_triangle_morphism

variable {C}

lemma mappingCone_triangleh_distinguished {X Y : CochainComplex C â„¤} (f : X âŸ¶ Y) :
    CochainComplex.mappingCone.triangleh f âˆˆ distTriang (HomotopyCategory _ _) :=
  âŸ¨_, _, f, âŸ¨Iso.refl _âŸ©âŸ©

end HomotopyCategory
