/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Homology.HomotopyCategory.MappingCone
import Mathlib.Algebra.Homology.HomotopyCategory.HomComplexShift
import Mathlib.CategoryTheory.Triangulated.Functor

/-! The pretriangulated structure on the homotopy category of complexes

In this file, we define the pretriangulated structure on the homotopy
category `HomotopyCategory C (ComplexShape.up ‚Ñ§)` of an additive category `C`.
The distinguished triangles are the triangles that are isomorphic to the
image in the homotopy category of the standard triangle
`K ‚ü∂ L ‚ü∂ mappingCone œÜ ‚ü∂ K‚ü¶(1 : ‚Ñ§)‚üß` for some morphism of
cochain complexes `œÜ : K ‚ü∂ L`.

This result first appeared in the Liquid Tensor Experiment. In the LTE, the
formalization followed the Stacks Project: in particular, the distinguished
triangles were defined using degreewise-split short exact sequences of cochain
complexes. Here, we follow the original definitions in [Verdiers's thesis, I.3][verdier1996]
(with the better sign conventions from the introduction of
[Brian Conrad's book *Grothendieck duality and base change*][conrad2000]).

## References
* [Jean-Louis Verdier, *Des cat√©gories d√©riv√©es des cat√©gories ab√©liennes*][verdier1996]
* [Brian Conrad, Grothendieck duality and base change][conrad2000]
* https://stacks.math.columbia.edu/tag/014P

-/

assert_not_exists TwoSidedIdeal

open CategoryTheory Category Limits CochainComplex.HomComplex Pretriangulated

variable {C D : Type*} [Category C] [Category D]
  [Preadditive C] [HasBinaryBiproducts C]
  [Preadditive D] [HasBinaryBiproducts D]
  {K L : CochainComplex C ‚Ñ§} (œÜ : K ‚ü∂ L)

namespace CochainComplex

namespace mappingCone

/-- The standard triangle `K ‚ü∂ L ‚ü∂ mappingCone œÜ ‚ü∂ K‚ü¶(1 : ‚Ñ§)‚üß` in `CochainComplex C ‚Ñ§`
attached to a morphism `œÜ : K ‚ü∂ L`. It involves `œÜ`, `inr œÜ : L ‚ü∂ mappingCone œÜ` and
the morphism induced by the `1`-cocycle `-mappingCone.fst œÜ`. -/
@[simps! obj‚ÇÅ obj‚ÇÇ obj‚ÇÉ mor‚ÇÅ mor‚ÇÇ]
noncomputable def triangle : Triangle (CochainComplex C ‚Ñ§) :=
  Triangle.mk œÜ (inr œÜ) (Cocycle.homOf ((-fst œÜ).rightShift 1 0 (zero_add 1)))

@[reassoc (attr := simp)]
lemma inl_v_triangle_mor‚ÇÉ_f (p q : ‚Ñ§) (hpq : p + (-1) = q) :
    (inl œÜ).v p q hpq ‚â´ (triangle œÜ).mor‚ÇÉ.f q =
      -(K.shiftFunctorObjXIso 1 q p (by rw [‚Üê hpq, neg_add_cancel_right])).inv := by
  dsimp [triangle]
  -- the following list of lemmas was obtained by doing
  -- simp? [Cochain.rightShift_v _ 1 0 (zero_add 1) q q (add_zero q) p (by omega)]
  simp only [Int.reduceNeg, Cochain.rightShift_neg, Cochain.neg_v, shiftFunctor_obj_X',
    Cochain.rightShift_v _ 1 0 (zero_add 1) q q (add_zero q) p (by cutsat), shiftFunctor_obj_X,
    shiftFunctorObjXIso, Preadditive.comp_neg, inl_v_fst_v_assoc]

@[reassoc (attr := simp)]
lemma inr_f_triangle_mor‚ÇÉ_f (p : ‚Ñ§) : (inr œÜ).f p ‚â´ (triangle œÜ).mor‚ÇÉ.f p = 0 := by
  dsimp [triangle]
  -- the following list of lemmas was obtained by doing
  -- simp? [Cochain.rightShift_v _ 1 0 _ p p (add_zero p) (p+1) rfl]
  simp only [Cochain.rightShift_neg, Cochain.neg_v, shiftFunctor_obj_X',
    Cochain.rightShift_v _ 1 0 _ p p (add_zero p) (p + 1) rfl, shiftFunctor_obj_X,
    shiftFunctorObjXIso, HomologicalComplex.XIsoOfEq_rfl, Iso.refl_inv, comp_id,
    Preadditive.comp_neg, inr_f_fst_v, neg_zero]

@[reassoc (attr := simp)]
lemma inr_triangleŒ¥ : inr œÜ ‚â´ (triangle œÜ).mor‚ÇÉ = 0 := by ext; simp

/-- The (distinguished) triangle in the homotopy category that is associated to
a morphism `œÜ : K ‚ü∂ L` in the category `CochainComplex C ‚Ñ§`. -/
noncomputable abbrev triangleh : Triangle (HomotopyCategory C (ComplexShape.up ‚Ñ§)) :=
  (HomotopyCategory.quotient _ _).mapTriangle.obj (triangle œÜ)

variable (K) in
/-- The mapping cone of the identity is contractible. -/
noncomputable def homotopyToZeroOfId : Homotopy (ùüô (mappingCone (ùüô K))) 0 :=
  descHomotopy (ùüô K) _ _ 0 (inl _) (by simp) (by simp)

section mapOfHomotopy

variable {K‚ÇÅ L‚ÇÅ K‚ÇÇ L‚ÇÇ K‚ÇÉ L‚ÇÉ : CochainComplex C ‚Ñ§} {œÜ‚ÇÅ : K‚ÇÅ ‚ü∂ L‚ÇÅ} {œÜ‚ÇÇ : K‚ÇÇ ‚ü∂ L‚ÇÇ}
  {a : K‚ÇÅ ‚ü∂ K‚ÇÇ} {b : L‚ÇÅ ‚ü∂ L‚ÇÇ} (H : Homotopy (œÜ‚ÇÅ ‚â´ b) (a ‚â´ œÜ‚ÇÇ))

/-- The morphism `mappingCone œÜ‚ÇÅ ‚ü∂ mappingCone œÜ‚ÇÇ` that is induced by a square that
is commutative up to homotopy. -/
noncomputable def mapOfHomotopy :
    mappingCone œÜ‚ÇÅ ‚ü∂ mappingCone œÜ‚ÇÇ :=
  desc œÜ‚ÇÅ ((Cochain.ofHom a).comp (inl œÜ‚ÇÇ) (zero_add _) +
    ((Cochain.equivHomotopy _ _) H).1.comp (Cochain.ofHom (inr œÜ‚ÇÇ)) (add_zero _))
    (b ‚â´ inr œÜ‚ÇÇ) (by simp)

@[reassoc]
lemma triangleMapOfHomotopy_comm‚ÇÇ :
    inr œÜ‚ÇÅ ‚â´ mapOfHomotopy H = b ‚â´ inr œÜ‚ÇÇ := by
  simp [mapOfHomotopy]

@[reassoc]
lemma triangleMapOfHomotopy_comm‚ÇÉ :
    mapOfHomotopy H ‚â´ (triangle œÜ‚ÇÇ).mor‚ÇÉ = (triangle œÜ‚ÇÅ).mor‚ÇÉ ‚â´ a‚ü¶1‚üß' := by
  ext p
  dsimp [mapOfHomotopy, triangle]
  -- the following list of lemmas as been obtained by doing
  -- simp? [ext_from_iff _ _ _ rfl, Cochain.rightShift_v _ 1 0 _ p p _ (p + 1) rfl]
  simp only [Int.reduceNeg, Cochain.rightShift_neg, Cochain.neg_v, shiftFunctor_obj_X',
    Cochain.rightShift_v _ 1 0 _ p p _ (p + 1) rfl, shiftFunctor_obj_X, shiftFunctorObjXIso,
    HomologicalComplex.XIsoOfEq_rfl, Iso.refl_inv, comp_id, Preadditive.comp_neg,
    Preadditive.neg_comp, ext_from_iff _ _ _ rfl, inl_v_desc_f_assoc, Cochain.add_v,
    Cochain.zero_cochain_comp_v, Cochain.ofHom_v, Cochain.comp_zero_cochain_v, Preadditive.add_comp,
    assoc, inl_v_fst_v, inr_f_fst_v, comp_zero, add_zero, inl_v_fst_v_assoc, inr_f_desc_f_assoc,
    HomologicalComplex.comp_f, neg_zero, inr_f_fst_v_assoc, zero_comp, and_self]

/-- The morphism `triangleh œÜ‚ÇÅ ‚ü∂ triangleh œÜ‚ÇÇ` that is induced by a square that
is commutative up to homotopy. -/
@[simps]
noncomputable def trianglehMapOfHomotopy :
    triangleh œÜ‚ÇÅ ‚ü∂ triangleh œÜ‚ÇÇ where
  hom‚ÇÅ := (HomotopyCategory.quotient _ _).map a
  hom‚ÇÇ := (HomotopyCategory.quotient _ _).map b
  hom‚ÇÉ := (HomotopyCategory.quotient _ _).map (mapOfHomotopy H)
  comm‚ÇÅ := by
    dsimp
    simp only [‚Üê Functor.map_comp]
    exact HomotopyCategory.eq_of_homotopy _ _ H
  comm‚ÇÇ := by
    dsimp
    simp only [‚Üê Functor.map_comp, triangleMapOfHomotopy_comm‚ÇÇ]
  comm‚ÇÉ := by
    dsimp
    rw [‚Üê Functor.map_comp_assoc, triangleMapOfHomotopy_comm‚ÇÉ, Functor.map_comp, assoc, assoc]
    simp

end mapOfHomotopy

section map

variable {K‚ÇÅ L‚ÇÅ K‚ÇÇ L‚ÇÇ K‚ÇÉ L‚ÇÉ : CochainComplex C ‚Ñ§} (œÜ‚ÇÅ : K‚ÇÅ ‚ü∂ L‚ÇÅ) (œÜ‚ÇÇ : K‚ÇÇ ‚ü∂ L‚ÇÇ) (œÜ‚ÇÉ : K‚ÇÉ ‚ü∂ L‚ÇÉ)
  (a : K‚ÇÅ ‚ü∂ K‚ÇÇ) (b : L‚ÇÅ ‚ü∂ L‚ÇÇ)

/-- The morphism `mappingCone œÜ‚ÇÅ ‚ü∂ mappingCone œÜ‚ÇÇ` that is induced by a commutative square. -/
noncomputable def map (comm : œÜ‚ÇÅ ‚â´ b = a ‚â´ œÜ‚ÇÇ) : mappingCone œÜ‚ÇÅ ‚ü∂ mappingCone œÜ‚ÇÇ :=
  desc œÜ‚ÇÅ ((Cochain.ofHom a).comp (inl œÜ‚ÇÇ) (zero_add _)) (b ‚â´ inr œÜ‚ÇÇ)
    (by simp [reassoc_of% comm])

variable (comm : œÜ‚ÇÅ ‚â´ b = a ‚â´ œÜ‚ÇÇ)

lemma map_eq_mapOfHomotopy : map œÜ‚ÇÅ œÜ‚ÇÇ a b comm = mapOfHomotopy (Homotopy.ofEq comm) := by
  simp [map, mapOfHomotopy]

lemma map_id : map œÜ œÜ (ùüô _) (ùüô _) (by rw [id_comp, comp_id]) = ùüô _ := by
  ext n
  simp [ext_from_iff _ (n + 1) n rfl, map]

variable (a' : K‚ÇÇ ‚ü∂ K‚ÇÉ) (b' : L‚ÇÇ ‚ü∂ L‚ÇÉ)

@[reassoc]
lemma map_comp (comm' : œÜ‚ÇÇ ‚â´ b' = a' ‚â´ œÜ‚ÇÉ) :
    map œÜ‚ÇÅ œÜ‚ÇÉ (a ‚â´ a') (b ‚â´ b') (by rw [reassoc_of% comm, comm', assoc]) =
      map œÜ‚ÇÅ œÜ‚ÇÇ a b comm ‚â´ map œÜ‚ÇÇ œÜ‚ÇÉ a' b' comm' := by
  ext n
  simp [ext_from_iff _ (n + 1) n rfl, map]

/-- The morphism `triangle œÜ‚ÇÅ ‚ü∂ triangle œÜ‚ÇÇ` that is induced by a commutative square. -/
@[simps]
noncomputable def triangleMap :
    triangle œÜ‚ÇÅ ‚ü∂ triangle œÜ‚ÇÇ where
  hom‚ÇÅ := a
  hom‚ÇÇ := b
  hom‚ÇÉ := map œÜ‚ÇÅ œÜ‚ÇÇ a b comm
  comm‚ÇÅ := comm
  comm‚ÇÇ := by
    dsimp
    rw [map_eq_mapOfHomotopy, triangleMapOfHomotopy_comm‚ÇÇ]
  comm‚ÇÉ := by
    dsimp
    rw [map_eq_mapOfHomotopy, triangleMapOfHomotopy_comm‚ÇÉ]

end map

section Rotate

/-- Given `œÜ : K ‚ü∂ L`, `K‚ü¶(1 : ‚Ñ§)‚üß` is homotopy equivalent to
the mapping cone of `inr œÜ : L ‚ü∂ mappingCone œÜ`. -/
noncomputable def rotateHomotopyEquiv :
    HomotopyEquiv (K‚ü¶(1 : ‚Ñ§)‚üß) (mappingCone (inr œÜ)) where
  hom := lift (inr œÜ) (-(Cocycle.ofHom œÜ).leftShift 1 1 (zero_add 1))
    (-(inl œÜ).leftShift 1 0 (neg_add_cancel 1)) (by
      -- the following list of lemmas has been obtained by doing
      -- simp? [Cochain.Œ¥_leftShift _ 1 0 1 (neg_add_cancel 1) 0 (zero_add 1)]
      simp only [Int.reduceNeg, Œ¥_neg,
        Cochain.Œ¥_leftShift _ 1 0 1 (neg_add_cancel 1) 0 (zero_add 1),
        Int.negOnePow_one, Œ¥_inl, Cochain.ofHom_comp, Cochain.leftShift_comp_zero_cochain,
        Units.neg_smul, one_smul, neg_neg, Cocycle.coe_neg, Cocycle.leftShift_coe,
        Cocycle.ofHom_coe, Cochain.neg_comp, add_neg_cancel])
  inv := desc (inr œÜ) 0 (triangle œÜ).mor‚ÇÉ
    (by simp only [Œ¥_zero, inr_triangleŒ¥, Cochain.ofHom_zero])
  homotopyHomInvId := Homotopy.ofEq (by
    ext n
    -- the following list of lemmas has been obtained by doing
    -- simp? [lift_desc_f _ _ _ _ _ _ _ _ _ rfl,
    --   (inl œÜ).leftShift_v 1 0 _ _ n _ (n + 1) (by simp only [add_neg_cancel_right])]
    simp only [shiftFunctor_obj_X', Int.reduceNeg, HomologicalComplex.comp_f,
      lift_desc_f _ _ _ _ _ _ _ _ _ rfl, Cocycle.coe_neg, Cocycle.leftShift_coe,
      Cocycle.ofHom_coe, Cochain.neg_v, Cochain.zero_v,
      comp_zero, (inl œÜ).leftShift_v 1 0 _ _ n _ (n + 1) (by simp only [add_neg_cancel_right]),
      shiftFunctor_obj_X, mul_zero, sub_self, Int.zero_ediv, add_zero, Int.negOnePow_zero,
      shiftFunctorObjXIso, HomologicalComplex.XIsoOfEq_rfl, Iso.refl_hom, id_comp, one_smul,
      Preadditive.neg_comp, inl_v_triangle_mor‚ÇÉ_f, Iso.refl_inv, neg_neg, zero_add,
      HomologicalComplex.id_f])
  homotopyInvHomId := (Cochain.equivHomotopy _ _).symm
    ‚ü®-(snd (inr œÜ)).comp ((snd œÜ).comp (inl (inr œÜ)) (zero_add (-1))) (zero_add (-1)), by
      ext n
      -- the following list of lemmas has been obtained by doing
      -- simp? [ext_to_iff _ _ (n + 1) rfl, ext_from_iff _ (n + 1) _ rfl,
      --   Œ¥_zero_cochain_comp _ _ _ (neg_add_cancel 1),
      --   (inl œÜ).leftShift_v 1 0 (neg_add_cancel 1) n n (add_zero n) (n + 1) (by omega),
      --   (Cochain.ofHom œÜ).leftShift_v 1 1 (zero_add 1) n (n + 1) rfl (n + 1) (by omega),
      --   Cochain.comp_v _ _ (add_neg_cancel 1) n (n + 1) n rfl (by omega)]
      simp only [Int.reduceNeg, Cochain.ofHom_comp, ofHom_desc, ofHom_lift, Cocycle.coe_neg,
        Cocycle.leftShift_coe, Cocycle.ofHom_coe, Cochain.comp_zero_cochain_v,
        shiftFunctor_obj_X', Œ¥_neg, Œ¥_zero_cochain_comp _ _ _ (neg_add_cancel 1), Œ¥_inl,
        Int.negOnePow_neg, Int.negOnePow_one, Œ¥_snd, Cochain.neg_comp,
        Cochain.comp_assoc_of_second_is_zero_cochain, smul_neg, Units.neg_smul, one_smul,
        neg_neg, Cochain.comp_add, inr_snd_assoc, neg_add_rev, Cochain.add_v, Cochain.neg_v,
        Cochain.comp_v _ _ (add_neg_cancel 1) n (n + 1) n rfl (by omega),
        Cochain.zero_cochain_comp_v, Cochain.ofHom_v, HomologicalComplex.id_f,
        ext_to_iff _ _ (n + 1) rfl, assoc, liftCochain_v_fst_v,
        (Cochain.ofHom œÜ).leftShift_v 1 1 (zero_add 1) n (n + 1) rfl (n + 1) (by omega),
        shiftFunctor_obj_X, mul_one, sub_self, mul_zero, Int.zero_ediv, add_zero,
        shiftFunctorObjXIso, HomologicalComplex.XIsoOfEq_rfl, Iso.refl_hom, id_comp,
        Preadditive.add_comp, Preadditive.neg_comp, inl_v_fst_v, comp_id, inr_f_fst_v, comp_zero,
        neg_zero, neg_add_cancel_comm, ext_from_iff _ (n + 1) _ rfl, inl_v_descCochain_v_assoc,
        Cochain.zero_v, zero_comp, Preadditive.comp_neg, inl_v_snd_v_assoc,
        inr_f_descCochain_v_assoc, inr_f_snd_v_assoc, inl_v_triangle_mor‚ÇÉ_f_assoc, triangle_obj‚ÇÅ,
        Iso.refl_inv, inl_v_fst_v_assoc, inr_f_triangle_mor‚ÇÉ_f_assoc, inr_f_fst_v_assoc, and_self,
        liftCochain_v_snd_v,
        (inl œÜ).leftShift_v 1 0 (neg_add_cancel 1) n n (add_zero n) (n + 1) (by omega),
        Int.negOnePow_zero, inl_v_snd_v, inr_f_snd_v, zero_add, inl_v_descCochain_v,
        inr_f_descCochain_v, inl_v_triangle_mor‚ÇÉ_f, inr_f_triangle_mor‚ÇÉ_f, neg_add_cancel]‚ü©

/-- Auxiliary definition for `rotateTrianglehIso`. -/
noncomputable def rotateHomotopyEquivComm‚ÇÇHomotopy :
    Homotopy ((triangle œÜ).mor‚ÇÉ ‚â´ (rotateHomotopyEquiv œÜ).hom)
      (inr (CochainComplex.mappingCone.inr œÜ)) := (Cochain.equivHomotopy _ _).symm
      ‚ü®-(snd œÜ).comp (inl (inr œÜ)) (zero_add (-1)), by
        ext p
        dsimp [rotateHomotopyEquiv]
        -- the following list of lemmas has been obtained by doing
        -- simp? [ext_from_iff _ _ _ rfl, ext_to_iff _ _ _ rfl,
        --  (inl œÜ).leftShift_v 1 0 (neg_add_cancel 1) p p (add_zero p) (p + 1) (by omega),
        --  Œ¥_zero_cochain_comp _ _ _ (neg_add_cancel 1),
        --  Cochain.comp_v _ _ (add_neg_cancel 1) p (p + 1) p rfl (by omega),
        --  (Cochain.ofHom œÜ).leftShift_v 1 1 (zero_add 1) p (p + 1) rfl (p + 1) (by omega)]‚ü©
        simp only [Int.reduceNeg, Cochain.ofHom_comp, ofHom_lift, Cocycle.coe_neg,
          Cocycle.leftShift_coe, Cocycle.ofHom_coe, Cochain.comp_zero_cochain_v,
          shiftFunctor_obj_X', Cochain.ofHom_v, Œ¥_neg, Œ¥_zero_cochain_comp _ _ _ (neg_add_cancel 1),
          Œ¥_inl, Int.negOnePow_neg, Int.negOnePow_one, Œ¥_snd, Cochain.neg_comp,
          Cochain.comp_assoc_of_second_is_zero_cochain, smul_neg, Units.neg_smul, one_smul, neg_neg,
          neg_add_rev, Cochain.add_v, Cochain.neg_v,
          Cochain.comp_v _ _ (add_neg_cancel 1) p (p + 1) p rfl (by cutsat),
          Cochain.zero_cochain_comp_v, ext_from_iff _ _ _ rfl, inl_v_triangle_mor‚ÇÉ_f_assoc,
          triangle_obj‚ÇÅ, shiftFunctor_obj_X, shiftFunctorObjXIso, HomologicalComplex.XIsoOfEq_rfl,
          Iso.refl_inv, Preadditive.neg_comp, id_comp, Preadditive.comp_add, Preadditive.comp_neg,
          inl_v_fst_v_assoc, inl_v_snd_v_assoc, zero_comp, neg_zero, add_zero, ext_to_iff _ _ _ rfl,
          liftCochain_v_fst_v,
          (Cochain.ofHom œÜ).leftShift_v 1 1 (zero_add 1) p (p + 1) rfl (p + 1) (by cutsat), mul_one,
          sub_self, mul_zero, Int.zero_ediv, Iso.refl_hom, Preadditive.add_comp, assoc, inl_v_fst_v,
          comp_id, inr_f_fst_v, comp_zero, liftCochain_v_snd_v,
          (inl œÜ).leftShift_v 1 0 (neg_add_cancel 1) p p (add_zero p) (p + 1) (by cutsat),
          Int.negOnePow_zero, inl_v_snd_v, inr_f_snd_v, zero_add, and_self,
          inr_f_triangle_mor‚ÇÉ_f_assoc, inr_f_fst_v_assoc, inr_f_snd_v_assoc, neg_add_cancel]‚ü©

@[reassoc (attr := simp)]
lemma rotateHomotopyEquiv_comm‚ÇÇ :
    (HomotopyCategory.quotient _ _ ).map (triangle œÜ).mor‚ÇÉ ‚â´
      (HomotopyCategory.quotient _ _ ).map (rotateHomotopyEquiv œÜ).hom =
      (HomotopyCategory.quotient _ _ ).map (inr (inr œÜ)) := by
  simpa only [Functor.map_comp]
    using HomotopyCategory.eq_of_homotopy _ _  (rotateHomotopyEquivComm‚ÇÇHomotopy œÜ)

@[reassoc (attr := simp)]
lemma rotateHomotopyEquiv_comm‚ÇÉ :
    (rotateHomotopyEquiv œÜ).hom ‚â´ (triangle (inr œÜ)).mor‚ÇÉ = -œÜ‚ü¶1‚üß' := by
  ext p
  dsimp [rotateHomotopyEquiv]
  -- the following list of lemmas has been obtained by doing
  -- simp? [lift_f _ _ _ _ _ (p + 1) rfl,
  --   (Cochain.ofHom œÜ).leftShift_v 1 1 (zero_add 1) p (p + 1) rfl (p + 1) (by omega)]
  simp only [Int.reduceNeg, lift_f _ _ _ _ _ (p + 1) rfl, shiftFunctor_obj_X', Cocycle.coe_neg,
    Cocycle.leftShift_coe, Cocycle.ofHom_coe, Cochain.neg_v,
    (Cochain.ofHom œÜ).leftShift_v 1 1 (zero_add 1) p (p + 1) rfl (p + 1) (by cutsat),
    shiftFunctor_obj_X, mul_one, sub_self, mul_zero, Int.zero_ediv, add_zero, Int.negOnePow_one,
    shiftFunctorObjXIso, HomologicalComplex.XIsoOfEq_rfl, Iso.refl_hom, Cochain.ofHom_v, id_comp,
    Units.neg_smul, one_smul, neg_neg, Preadditive.neg_comp, Preadditive.add_comp, assoc,
    inl_v_triangle_mor‚ÇÉ_f, Iso.refl_inv, Preadditive.comp_neg, comp_id, inr_f_triangle_mor‚ÇÉ_f,
    comp_zero, neg_zero]

/-- The canonical isomorphism of triangles `(triangleh œÜ).rotate ‚âÖ (triangleh (inr œÜ))`. -/
noncomputable def rotateTrianglehIso :
    (triangleh œÜ).rotate ‚âÖ (triangleh (inr œÜ)) :=
  Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _)
    (((HomotopyCategory.quotient C (ComplexShape.up ‚Ñ§)).commShiftIso (1 : ‚Ñ§)).symm.app K ‚â™‚â´
      HomotopyCategory.isoOfHomotopyEquiv (rotateHomotopyEquiv œÜ))
        (by simp) (by simp) (by
        dsimp
        rw [CategoryTheory.Functor.map_id, comp_id, assoc, ‚Üê Functor.map_comp_assoc,
          rotateHomotopyEquiv_comm‚ÇÉ, Functor.map_neg, Preadditive.neg_comp,
          Functor.commShiftIso_hom_naturality, Preadditive.comp_neg,
          Iso.inv_hom_id_app_assoc])

end Rotate

section Shift

/-- The canonical isomorphism `(mappingCone œÜ)‚ü¶n‚üß ‚âÖ mappingCone (œÜ‚ü¶n‚üß')`. -/
noncomputable def shiftIso (n : ‚Ñ§) : (mappingCone œÜ)‚ü¶n‚üß ‚âÖ mappingCone (œÜ‚ü¶n‚üß') where
  hom := lift _ (n.negOnePow ‚Ä¢ (fst œÜ).shift n) ((snd œÜ).shift n) (by
    ext p q hpq
    dsimp
    simp only [Cochain.Œ¥_shift, Œ¥_snd, Cochain.shift_neg, smul_neg, Cochain.neg_v,
      shiftFunctor_obj_X', Cochain.units_smul_v, Cochain.shift_v', Cochain.comp_zero_cochain_v,
      Cochain.ofHom_v, Cochain.units_smul_comp, shiftFunctor_map_f', neg_add_cancel])
  inv := desc _ (n.negOnePow ‚Ä¢ (inl œÜ).shift n) ((inr œÜ)‚ü¶n‚üß') (by
    ext p
    dsimp
    simp only [Int.reduceNeg, Œ¥_units_smul, Cochain.Œ¥_shift, Œ¥_inl, Cochain.ofHom_comp, smul_smul,
      Int.units_mul_self, one_smul, Cochain.shift_v', Cochain.comp_zero_cochain_v,
      Cochain.ofHom_v, shiftFunctor_obj_X', shiftFunctor_map_f'])
  hom_inv_id := by
    ext p
    dsimp
    simp only [Int.reduceNeg, lift_desc_f _ _ _ _ _ _ _ _ (p + 1) rfl, shiftFunctor_obj_X',
      Cocycle.coe_units_smul, Cocycle.shift_coe, Cochain.units_smul_v, Cochain.shift_v',
      Linear.comp_units_smul, Linear.units_smul_comp, smul_smul, Int.units_mul_self, one_smul,
      shiftFunctor_map_f', id_X]
  inv_hom_id := by
    ext p
    dsimp
    simp only [Int.reduceNeg, ext_from_iff _ (p + 1) _ rfl, shiftFunctor_obj_X',
      inl_v_desc_f_assoc, Cochain.units_smul_v, Cochain.shift_v', Linear.units_smul_comp, comp_id,
      ext_to_iff _ _ (p + 1) rfl, assoc, lift_f_fst_v,
      Cocycle.coe_units_smul, Cocycle.shift_coe, Linear.comp_units_smul, inl_v_fst_v, smul_smul,
      Int.units_mul_self, one_smul, lift_f_snd_v, inl_v_snd_v, smul_zero, and_self,
      inr_f_desc_f_assoc, shiftFunctor_map_f', inr_f_fst_v, inr_f_snd_v]

/-- The canonical isomorphism `(triangle œÜ)‚ü¶n‚üß ‚âÖ triangle (œÜ‚ü¶n‚üß')`. -/
noncomputable def shiftTriangleIso (n : ‚Ñ§) :
    (Triangle.shiftFunctor _ n).obj (triangle œÜ) ‚âÖ triangle (œÜ‚ü¶n‚üß') := by
  refine Triangle.isoMk _ _ (Iso.refl _) (n.negOnePow ‚Ä¢ Iso.refl _) (shiftIso œÜ n) ?_ ?_ ?_
  ¬∑ dsimp
    simp only [Linear.comp_units_smul, comp_id, id_comp, smul_smul,
      Int.units_mul_self, one_smul]
  ¬∑ ext p
    dsimp
    simp only [Units.smul_def, shiftIso, Int.reduceNeg, Linear.smul_comp, id_comp,
      ext_to_iff _ _ (p + 1) rfl, shiftFunctor_obj_X', assoc, lift_f_fst_v, Cocycle.coe_smul,
      Cocycle.shift_coe, Cochain.smul_v, Cochain.shift_v', Linear.comp_smul, inr_f_fst_v,
      smul_zero, lift_f_snd_v, inr_f_snd_v, and_true]
  ¬∑ ext p
    dsimp
    simp only [triangle, Triangle.mk_mor‚ÇÉ, Cocycle.homOf_f, Cocycle.rightShift_coe,
      Cocycle.coe_neg, Cochain.rightShift_neg, Cochain.neg_v, shiftFunctor_obj_X',
      (fst œÜ).1.rightShift_v 1 0 (zero_add 1) (p + n) (p + n) (add_zero (p + n)) (p + 1 + n)
        (by cutsat),
      shiftFunctor_obj_X, shiftFunctorObjXIso, shiftFunctorComm_hom_app_f, Preadditive.neg_comp,
      assoc, Iso.inv_hom_id, comp_id, smul_neg, Units.smul_def, shiftIso, Int.reduceNeg,
      (fst (œÜ‚ü¶n‚üß')).1.rightShift_v 1 0 (zero_add 1) p p (add_zero p) (p + 1) rfl,
      HomologicalComplex.XIsoOfEq_rfl, Iso.refl_inv, Preadditive.comp_neg, lift_f_fst_v,
      Cocycle.coe_smul, Cocycle.shift_coe, Cochain.smul_v, Cochain.shift_v']

/-- The canonical isomorphism `(triangleh œÜ)‚ü¶n‚üß ‚âÖ triangleh (œÜ‚ü¶n‚üß')`. -/
noncomputable def shiftTrianglehIso (n : ‚Ñ§) :
    (Triangle.shiftFunctor _ n).obj (triangleh œÜ) ‚âÖ triangleh (œÜ‚ü¶n‚üß') :=
  ((HomotopyCategory.quotient _ _).mapTriangle.commShiftIso n).symm.app _ ‚â™‚â´
    (HomotopyCategory.quotient _ _).mapTriangle.mapIso (shiftTriangleIso œÜ n)

end Shift

section

open Preadditive

variable (G : C ‚•§ D) [G.Additive]

lemma map_Œ¥ :
    (G.mapHomologicalComplex (ComplexShape.up ‚Ñ§)).map (triangle œÜ).mor‚ÇÉ ‚â´
      NatTrans.app ((Functor.mapHomologicalComplex G (ComplexShape.up ‚Ñ§)).commShiftIso  1).hom K =
    (mapHomologicalComplexIso œÜ G).hom ‚â´
      (triangle ((G.mapHomologicalComplex (ComplexShape.up ‚Ñ§)).map œÜ)).mor‚ÇÉ := by
  ext n
  dsimp [mapHomologicalComplexIso]
  rw [mapHomologicalComplexXIso_eq œÜ G n (n + 1) rfl, mapHomologicalComplexXIso'_hom]
  simp only [Functor.mapHomologicalComplex_obj_X, add_comp, assoc, inl_v_triangle_mor‚ÇÉ_f,
    shiftFunctor_obj_X, shiftFunctorObjXIso, HomologicalComplex.XIsoOfEq_rfl, Iso.refl_inv,
    comp_neg, comp_id, inr_f_triangle_mor‚ÇÉ_f, comp_zero, add_zero]
  dsimp [triangle]
  rw [Cochain.rightShift_v _ 1 0 (by cutsat) n n (by cutsat) (n + 1) (by cutsat)]
  simp only [shiftFunctor_obj_X, Cochain.neg_v, shiftFunctorObjXIso,
    HomologicalComplex.XIsoOfEq_rfl, Iso.refl_inv, comp_id, Functor.map_neg]

/-- If `œÜ : K ‚ü∂ L` is a morphism of cochain complexes in `C` and `G : C ‚•§ D` is an
additive functor, then the image by `G` of the triangle `triangle œÜ` identifies to
the triangle associated to the image of `œÜ` by `G`. -/
noncomputable def mapTriangleIso :
    (G.mapHomologicalComplex (ComplexShape.up ‚Ñ§)).mapTriangle.obj (triangle œÜ) ‚âÖ
      triangle ((G.mapHomologicalComplex (ComplexShape.up ‚Ñ§)).map œÜ) := by
  refine Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) (mapHomologicalComplexIso œÜ G) ?_ ?_ ?_
  ¬∑ dsimp
    simp only [comp_id, id_comp]
  ¬∑ dsimp
    rw [map_inr, id_comp]
  ¬∑ dsimp
    simp only [CategoryTheory.Functor.map_id, comp_id, map_Œ¥]

/-- If `œÜ : K ‚ü∂ L` is a morphism of cochain complexes in `C` and `G : C ‚•§ D` is an
additive functor, then the image by `G` of the triangle `triangleh œÜ` identifies to
the triangle associated to the image of `œÜ` by `G`. -/
noncomputable def mapTrianglehIso :
    (G.mapHomotopyCategory (ComplexShape.up ‚Ñ§)).mapTriangle.obj (triangleh œÜ) ‚âÖ
      triangleh ((G.mapHomologicalComplex (ComplexShape.up ‚Ñ§)).map œÜ) :=
  (Functor.mapTriangleCompIso _ _).symm.app _ ‚â™‚â´
    (Functor.mapTriangleIso (G.mapHomotopyCategoryFactors (ComplexShape.up ‚Ñ§))).app _ ‚â™‚â´
    (Functor.mapTriangleCompIso _ _).app _ ‚â™‚â´
    (HomotopyCategory.quotient D (ComplexShape.up ‚Ñ§)).mapTriangle.mapIso
      (CochainComplex.mappingCone.mapTriangleIso œÜ G)

end

end mappingCone

end CochainComplex

namespace HomotopyCategory

variable (C)

namespace Pretriangulated

/-- A triangle in `HomotopyCategory C (ComplexShape.up ‚Ñ§)` is distinguished if it is isomorphic to
the triangle `CochainComplex.mappingCone.triangleh œÜ` for some morphism of cochain
complexes `œÜ`. -/
def distinguishedTriangles : Set (Triangle (HomotopyCategory C (ComplexShape.up ‚Ñ§))) :=
  fun T => ‚àÉ (X Y : CochainComplex C ‚Ñ§) (œÜ : X ‚ü∂ Y),
    Nonempty (T ‚âÖ CochainComplex.mappingCone.triangleh œÜ)

variable {C}

lemma isomorphic_distinguished (T‚ÇÅ : Triangle (HomotopyCategory C (ComplexShape.up ‚Ñ§)))
    (hT‚ÇÅ : T‚ÇÅ ‚àà distinguishedTriangles C) (T‚ÇÇ : Triangle (HomotopyCategory C (ComplexShape.up ‚Ñ§)))
    (e : T‚ÇÇ ‚âÖ T‚ÇÅ) : T‚ÇÇ ‚àà distinguishedTriangles C := by
  obtain ‚ü®X, Y, f, ‚ü®e'‚ü©‚ü© := hT‚ÇÅ
  exact ‚ü®X, Y, f, ‚ü®e ‚â™‚â´ e'‚ü©‚ü©

variable [HasZeroObject C] in
lemma contractible_distinguished (X : HomotopyCategory C (ComplexShape.up ‚Ñ§)) :
    Pretriangulated.contractibleTriangle X ‚àà distinguishedTriangles C := by
  obtain ‚ü®X‚ü© := X
  refine ‚ü®_, _, ùüô X, ‚ü®?_‚ü©‚ü©
  have h := (isZero_quotient_obj_iff _).2 ‚ü®CochainComplex.mappingCone.homotopyToZeroOfId X‚ü©
  exact Triangle.isoMk _ _ (Iso.refl _) (Iso.refl _) h.isoZero.symm
    (by simp) (h.eq_of_tgt _ _) (by dsimp; ext)

lemma distinguished_cocone_triangle {X Y : HomotopyCategory C (ComplexShape.up ‚Ñ§)} (f : X ‚ü∂ Y) :
    ‚àÉ (Z : HomotopyCategory C (ComplexShape.up ‚Ñ§)) (g : Y ‚ü∂ Z) (h : Z ‚ü∂ X‚ü¶1‚üß),
      Triangle.mk f g h ‚àà distinguishedTriangles C := by
  obtain ‚ü®f, rfl‚ü© := (quotient _ _).map_surjective f
  exact ‚ü®_, _, _, ‚ü®_, _, f, ‚ü®Iso.refl _‚ü©‚ü©‚ü©

lemma rotate_distinguished_triangle' (T : Triangle (HomotopyCategory C (ComplexShape.up ‚Ñ§)))
    (hT : T ‚àà distinguishedTriangles C) : T.rotate ‚àà distinguishedTriangles C := by
  obtain ‚ü®K, L, œÜ, ‚ü®e‚ü©‚ü© := hT
  exact ‚ü®_, _, _, ‚ü®(rotate _).mapIso e ‚â™‚â´ CochainComplex.mappingCone.rotateTrianglehIso œÜ‚ü©‚ü©

lemma shift_distinguished_triangle (T : Triangle (HomotopyCategory C (ComplexShape.up ‚Ñ§)))
    (hT : T ‚àà distinguishedTriangles C) (n : ‚Ñ§) :
      (Triangle.shiftFunctor _ n).obj T ‚àà distinguishedTriangles C := by
  obtain ‚ü®K, L, œÜ, ‚ü®e‚ü©‚ü© := hT
  exact ‚ü®_, _, _, ‚ü®Functor.mapIso _ e ‚â™‚â´ CochainComplex.mappingCone.shiftTrianglehIso œÜ n‚ü©‚ü©

lemma invRotate_distinguished_triangle' (T : Triangle (HomotopyCategory C (ComplexShape.up ‚Ñ§)))
    (hT : T ‚àà distinguishedTriangles C) : T.invRotate ‚àà distinguishedTriangles C :=
  isomorphic_distinguished _
    (shift_distinguished_triangle _ (rotate_distinguished_triangle' _
      (rotate_distinguished_triangle' _ hT)) _) _
    ((invRotateIsoRotateRotateShiftFunctorNegOne _).app T)

lemma rotate_distinguished_triangle (T : Triangle (HomotopyCategory C (ComplexShape.up ‚Ñ§))) :
    T ‚àà distinguishedTriangles C ‚Üî T.rotate ‚àà distinguishedTriangles C := by
  constructor
  ¬∑ exact rotate_distinguished_triangle' T
  ¬∑ intro hT
    exact isomorphic_distinguished _ (invRotate_distinguished_triangle' T.rotate hT) _
      ((triangleRotation _).unitIso.app T)

open CochainComplex.mappingCone in
lemma complete_distinguished_triangle_morphism
    (T‚ÇÅ T‚ÇÇ : Triangle (HomotopyCategory C (ComplexShape.up ‚Ñ§)))
    (hT‚ÇÅ : T‚ÇÅ ‚àà distinguishedTriangles C) (hT‚ÇÇ : T‚ÇÇ ‚àà distinguishedTriangles C)
    (a : T‚ÇÅ.obj‚ÇÅ ‚ü∂ T‚ÇÇ.obj‚ÇÅ) (b : T‚ÇÅ.obj‚ÇÇ ‚ü∂ T‚ÇÇ.obj‚ÇÇ) (fac : T‚ÇÅ.mor‚ÇÅ ‚â´ b = a ‚â´ T‚ÇÇ.mor‚ÇÅ) :
    ‚àÉ (c : T‚ÇÅ.obj‚ÇÉ ‚ü∂ T‚ÇÇ.obj‚ÇÉ), T‚ÇÅ.mor‚ÇÇ ‚â´ c = b ‚â´ T‚ÇÇ.mor‚ÇÇ ‚àß
      T‚ÇÅ.mor‚ÇÉ ‚â´ a‚ü¶(1 : ‚Ñ§)‚üß' = c ‚â´ T‚ÇÇ.mor‚ÇÉ := by
  obtain ‚ü®K‚ÇÅ, L‚ÇÅ, œÜ‚ÇÅ, ‚ü®e‚ÇÅ‚ü©‚ü© := hT‚ÇÅ
  obtain ‚ü®K‚ÇÇ, L‚ÇÇ, œÜ‚ÇÇ, ‚ü®e‚ÇÇ‚ü©‚ü© := hT‚ÇÇ
  obtain ‚ü®a', ha'‚ü© : ‚àÉ (a' : (quotient _ _).obj K‚ÇÅ ‚ü∂ (quotient _ _).obj K‚ÇÇ),
    a' = e‚ÇÅ.inv.hom‚ÇÅ ‚â´ a ‚â´ e‚ÇÇ.hom.hom‚ÇÅ := ‚ü®_, rfl‚ü©
  obtain ‚ü®b', hb'‚ü© : ‚àÉ (b' : (quotient _ _).obj L‚ÇÅ ‚ü∂ (quotient _ _).obj L‚ÇÇ),
    b' = e‚ÇÅ.inv.hom‚ÇÇ ‚â´ b ‚â´ e‚ÇÇ.hom.hom‚ÇÇ := ‚ü®_, rfl‚ü©
  obtain ‚ü®a'', rfl‚ü© := (quotient _ _).map_surjective a'
  obtain ‚ü®b'', rfl‚ü© := (quotient _ _).map_surjective b'
  have H : Homotopy (œÜ‚ÇÅ ‚â´ b'') (a'' ‚â´ œÜ‚ÇÇ) := homotopyOfEq _ _ (by
    have comm‚ÇÅ‚ÇÅ := e‚ÇÅ.inv.comm‚ÇÅ
    have comm‚ÇÅ‚ÇÇ := e‚ÇÇ.hom.comm‚ÇÅ
    dsimp at comm‚ÇÅ‚ÇÅ comm‚ÇÅ‚ÇÇ
    simp only [Functor.map_comp, ha', hb', reassoc_of% comm‚ÇÅ‚ÇÅ,
      reassoc_of% fac, comm‚ÇÅ‚ÇÇ, assoc])
  let Œ≥ := e‚ÇÅ.hom ‚â´ trianglehMapOfHomotopy H ‚â´ e‚ÇÇ.inv
  have comm‚ÇÇ := Œ≥.comm‚ÇÇ
  have comm‚ÇÉ := Œ≥.comm‚ÇÉ
  dsimp [Œ≥] at comm‚ÇÇ comm‚ÇÉ
  simp only [ha', hb'] at comm‚ÇÇ comm‚ÇÉ
  refine ‚ü®Œ≥.hom‚ÇÉ, ?_, ?_‚ü©
  -- the following list of lemmas was obtained by doing simpa? [Œ≥] using comm‚ÇÇ
  ¬∑ simpa only [triangleCategory_comp, Functor.mapTriangle_obj, triangle_obj‚ÇÅ, triangle_obj‚ÇÇ,
      triangle_obj‚ÇÉ, triangle_mor‚ÇÅ, triangle_mor‚ÇÇ, TriangleMorphism.comp_hom‚ÇÉ, Triangle.mk_obj‚ÇÉ,
      trianglehMapOfHomotopy_hom‚ÇÉ, TriangleMorphism.comm‚ÇÇ_assoc, Triangle.mk_obj‚ÇÇ,
      Triangle.mk_mor‚ÇÇ, assoc, Iso.hom_inv_id_triangle_hom‚ÇÇ, comp_id,
      Iso.hom_inv_id_triangle_hom‚ÇÇ_assoc, Œ≥] using comm‚ÇÇ
  -- the following list of lemmas was obtained by doing simpa? [Œ≥] using comm‚ÇÉ
  ¬∑ simpa only [triangleCategory_comp, Functor.mapTriangle_obj, triangle_obj‚ÇÅ, triangle_obj‚ÇÇ,
      triangle_obj‚ÇÉ, triangle_mor‚ÇÅ, triangle_mor‚ÇÇ, TriangleMorphism.comp_hom‚ÇÉ, Triangle.mk_obj‚ÇÉ,
      trianglehMapOfHomotopy_hom‚ÇÉ, assoc, Triangle.mk_obj‚ÇÅ, Iso.hom_inv_id_triangle_hom‚ÇÅ, comp_id,
      Iso.hom_inv_id_triangle_hom‚ÇÅ_assoc, Œ≥] using comm‚ÇÉ

end Pretriangulated

variable [HasZeroObject C]

noncomputable instance : Pretriangulated (HomotopyCategory C (ComplexShape.up ‚Ñ§)) where
  distinguishedTriangles := Pretriangulated.distinguishedTriangles C
  isomorphic_distinguished := Pretriangulated.isomorphic_distinguished
  contractible_distinguished := Pretriangulated.contractible_distinguished
  distinguished_cocone_triangle := Pretriangulated.distinguished_cocone_triangle
  rotate_distinguished_triangle := Pretriangulated.rotate_distinguished_triangle
  complete_distinguished_triangle_morphism :=
    Pretriangulated.complete_distinguished_triangle_morphism

variable {C}

lemma mappingCone_triangleh_distinguished {X Y : CochainComplex C ‚Ñ§} (f : X ‚ü∂ Y) :
    CochainComplex.mappingCone.triangleh f ‚àà distTriang (HomotopyCategory _ _) :=
  ‚ü®_, _, f, ‚ü®Iso.refl _‚ü©‚ü©

variable [HasZeroObject D]

instance (G : C ‚•§ D) [G.Additive] :
    (G.mapHomotopyCategory (ComplexShape.up ‚Ñ§)).IsTriangulated where
  map_distinguished := by
    rintro T ‚ü®K, L, f, ‚ü®e‚ü©‚ü©
    exact ‚ü®_, _, _, ‚ü®(G.mapHomotopyCategory (ComplexShape.up ‚Ñ§)).mapTriangle.mapIso e ‚â™‚â´
      CochainComplex.mappingCone.mapTrianglehIso f G‚ü©‚ü©

end HomotopyCategory
