/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Scott Morrison
-/
import Mathlib.Algebra.Group.Defs
import Mathlib.Logic.Relation

#align_import algebra.homology.complex_shape from "leanprover-community/mathlib"@"c4658a649d216f57e99621708b09dcb3dcccbd23"

/-!
# Shapes of homological complexes

We define a structure `ComplexShape Œπ` for describing the shapes of homological complexes
indexed by a type `Œπ`.
This is intended to capture chain complexes and cochain complexes, indexed by either `‚Ñï` or `‚Ñ§`,
as well as more exotic examples.

Rather than insisting that the indexing type has a `succ` function
specifying where differentials should go,
inside `c : ComplexShape` we have `c.Rel : Œπ ‚Üí Œπ ‚Üí Prop`,
and when we define `HomologicalComplex`
we only allow nonzero differentials `d i j` from `i` to `j` if `c.Rel i j`.
Further, we require that `{ j // c.Rel i j }` and `{ i // c.Rel i j }` are subsingletons.
This means that the shape consists of some union of lines, rays, intervals, and circles.

Convenience functions `c.next` and `c.prev` provide these related elements
when they exist, and return their input otherwise.

This design aims to avoid certain problems arising from dependent type theory.
In particular we never have to ensure morphisms `d i : X i ‚ü∂ X (succ i)` compose as
expected (which would often require rewriting by equations in the indexing type).
Instead such identities become separate proof obligations when verifying that a
complex we've constructed is of the desired shape.

If `Œ±` is an `AddRightCancelSemigroup`, then we define `up Œ± : ComplexShape Œ±`,
the shape appropriate for cohomology, so `d : X i ‚ü∂ X j` is nonzero only when `j = i + 1`,
as well as `down Œ± : ComplexShape Œ±`, appropriate for homology,
so `d : X i ‚ü∂ X j` is nonzero only when `i = j + 1`.
(Later we'll introduce `CochainComplex` and `ChainComplex` as abbreviations for
`HomologicalComplex` with one of these shapes baked in.)
-/

open Classical

noncomputable section

/-- A `c : ComplexShape Œπ` describes the shape of a chain complex,
with chain groups indexed by `Œπ`.
Typically `Œπ` will be `‚Ñï`, `‚Ñ§`, or `Fin n`.

There is a relation `Rel : Œπ ‚Üí Œπ ‚Üí Prop`,
and we will only allow a non-zero differential from `i` to `j` when `Rel i j`.

There are axioms which imply `{ j // c.Rel i j }` and `{ i // c.Rel i j }` are subsingletons.
This means that the shape consists of some union of lines, rays, intervals, and circles.

Below we define `c.next` and `c.prev` which provide these related elements.
-/
@[ext]
structure ComplexShape (Œπ : Type*) where
  /-- Nonzero differentials `X i ‚ü∂ X j` shall be allowed
    on homological complexes when `Rel i j` holds. -/
  Rel : Œπ ‚Üí Œπ ‚Üí Prop
  /-- There is at most one nonzero differential from `X i`. -/
  next_eq : ‚àÄ {i j j'}, Rel i j ‚Üí Rel i j' ‚Üí j = j'
  /-- There is at most one nonzero differential to `X j`. -/
  prev_eq : ‚àÄ {i i' j}, Rel i j ‚Üí Rel i' j ‚Üí i = i'
#align complex_shape ComplexShape
#align complex_shape.ext ComplexShape.ext
#align complex_shape.ext_iff ComplexShape.ext_iff

namespace ComplexShape

variable {Œπ : Type*}

/-- The complex shape where only differentials from each `X.i` to itself are allowed.

This is mostly only useful so we can describe the relation of "related in `k` steps" below.
-/
@[simps]
def refl (Œπ : Type*) : ComplexShape Œπ where
  Rel i j := i = j
  next_eq w w' := w.symm.trans w'
  prev_eq w w' := w.trans w'.symm
#align complex_shape.refl ComplexShape.refl
#align complex_shape.refl_rel ComplexShape.refl_Rel

/-- The reverse of a `ComplexShape`.
-/
@[simps]
def symm (c : ComplexShape Œπ) : ComplexShape Œπ where
  Rel i j := c.Rel j i
  next_eq w w' := c.prev_eq w w'
  prev_eq w w' := c.next_eq w w'
#align complex_shape.symm ComplexShape.symm
#align complex_shape.symm_rel ComplexShape.symm_Rel

@[simp]
theorem symm_symm (c : ComplexShape Œπ) : c.symm.symm = c := by
  ext
  -- ‚ä¢ Rel (symm (symm c)) x‚úù¬π x‚úù ‚Üî Rel c x‚úù¬π x‚úù
  simp
  -- üéâ no goals
#align complex_shape.symm_symm ComplexShape.symm_symm

/-- The "composition" of two `ComplexShape`s.

We need this to define "related in k steps" later.
-/
@[simp]
def trans (c‚ÇÅ c‚ÇÇ : ComplexShape Œπ) : ComplexShape Œπ where
  Rel := Relation.Comp c‚ÇÅ.Rel c‚ÇÇ.Rel
  next_eq w w' := by
    obtain ‚ü®k, w‚ÇÅ, w‚ÇÇ‚ü© := w
    -- ‚ä¢ j‚úù = j'‚úù
    obtain ‚ü®k', w‚ÇÅ', w‚ÇÇ'‚ü© := w'
    -- ‚ä¢ j‚úù = j'‚úù
    rw [c‚ÇÅ.next_eq w‚ÇÅ w‚ÇÅ'] at w‚ÇÇ
    -- ‚ä¢ j‚úù = j'‚úù
    exact c‚ÇÇ.next_eq w‚ÇÇ w‚ÇÇ'
    -- üéâ no goals
  prev_eq w w' := by
    obtain ‚ü®k, w‚ÇÅ, w‚ÇÇ‚ü© := w
    -- ‚ä¢ i‚úù = i'‚úù
    obtain ‚ü®k', w‚ÇÅ', w‚ÇÇ'‚ü© := w'
    -- ‚ä¢ i‚úù = i'‚úù
    rw [c‚ÇÇ.prev_eq w‚ÇÇ w‚ÇÇ'] at w‚ÇÅ
    -- ‚ä¢ i‚úù = i'‚úù
    exact c‚ÇÅ.prev_eq w‚ÇÅ w‚ÇÅ'
    -- üéâ no goals
#align complex_shape.trans ComplexShape.trans

instance subsingleton_next (c : ComplexShape Œπ) (i : Œπ) : Subsingleton { j // c.Rel i j } := by
  constructor
  -- ‚ä¢ ‚àÄ (a b : { j // Rel c i j }), a = b
  rintro ‚ü®j, rij‚ü© ‚ü®k, rik‚ü©
  -- ‚ä¢ { val := j, property := rij } = { val := k, property := rik }
  congr
  -- ‚ä¢ j = k
  exact c.next_eq rij rik
  -- üéâ no goals

instance subsingleton_prev (c : ComplexShape Œπ) (j : Œπ) : Subsingleton { i // c.Rel i j } := by
  constructor
  -- ‚ä¢ ‚àÄ (a b : { i // Rel c i j }), a = b
  rintro ‚ü®i, rik‚ü© ‚ü®j, rjk‚ü©
  -- ‚ä¢ { val := i, property := rik } = { val := j, property := rjk }
  congr
  -- ‚ä¢ i = j
  exact c.prev_eq rik rjk
  -- üéâ no goals

/-- An arbitrary choice of index `j` such that `Rel i j`, if such exists.
Returns `i` otherwise.
-/
def next (c : ComplexShape Œπ) (i : Œπ) : Œπ :=
  if h : ‚àÉ j, c.Rel i j then h.choose else i
#align complex_shape.next ComplexShape.next

/-- An arbitrary choice of index `i` such that `Rel i j`, if such exists.
Returns `j` otherwise.
-/
def prev (c : ComplexShape Œπ) (j : Œπ) : Œπ :=
  if h : ‚àÉ i, c.Rel i j then h.choose else j
#align complex_shape.prev ComplexShape.prev

theorem next_eq' (c : ComplexShape Œπ) {i j : Œπ} (h : c.Rel i j) : c.next i = j := by
  apply c.next_eq _ h
  -- ‚ä¢ Rel c i (next c i)
  rw [next]
  -- ‚ä¢ Rel c i (if h : ‚àÉ j, Rel c i j then Exists.choose h else i)
  rw [dif_pos]
  -- ‚ä¢ Rel c i (Exists.choose ?hc)
  exact Exists.choose_spec ‚ü®j, h‚ü©
  -- üéâ no goals
#align complex_shape.next_eq' ComplexShape.next_eq'

theorem prev_eq' (c : ComplexShape Œπ) {i j : Œπ} (h : c.Rel i j) : c.prev j = i := by
  apply c.prev_eq _ h
  -- ‚ä¢ Rel c (prev c j) j
  rw [prev, dif_pos]
  -- ‚ä¢ Rel c (Exists.choose ?hc) j
  exact Exists.choose_spec (‚ü®i, h‚ü© : ‚àÉ k, c.Rel k j)
  -- üéâ no goals
#align complex_shape.prev_eq' ComplexShape.prev_eq'

/-- The `ComplexShape` allowing differentials from `X i` to `X (i+a)`.
(For example when `a = 1`, a cohomology theory indexed by `‚Ñï` or `‚Ñ§`)
-/
@[simps]
def up' {Œ± : Type*} [AddRightCancelSemigroup Œ±] (a : Œ±) : ComplexShape Œ± where
  Rel i j := i + a = j
  next_eq hi hj := hi.symm.trans hj
  prev_eq hi hj := add_right_cancel (hi.trans hj.symm)
#align complex_shape.up' ComplexShape.up'
#align complex_shape.up'_rel ComplexShape.up'_Rel

/-- The `ComplexShape` allowing differentials from `X (j+a)` to `X j`.
(For example when `a = 1`, a homology theory indexed by `‚Ñï` or `‚Ñ§`)
-/
@[simps]
def down' {Œ± : Type*} [AddRightCancelSemigroup Œ±] (a : Œ±) : ComplexShape Œ± where
  Rel i j := j + a = i
  next_eq hi hj := add_right_cancel (hi.trans hj.symm)
  prev_eq hi hj := hi.symm.trans hj
#align complex_shape.down' ComplexShape.down'
#align complex_shape.down'_rel ComplexShape.down'_Rel

theorem down'_mk {Œ± : Type*} [AddRightCancelSemigroup Œ±] (a : Œ±) (i j : Œ±) (h : j + a = i) :
  (down' a).Rel i j := h
#align complex_shape.down'_mk ComplexShape.down'_mk

/-- The `ComplexShape` appropriate for cohomology, so `d : X i ‚ü∂ X j` only when `j = i + 1`.
-/
@[simps!]
def up (Œ± : Type*) [AddRightCancelSemigroup Œ±] [One Œ±] : ComplexShape Œ± :=
  up' 1
#align complex_shape.up ComplexShape.up
#align complex_shape.up_rel ComplexShape.up_Rel

/-- The `ComplexShape` appropriate for homology, so `d : X i ‚ü∂ X j` only when `i = j + 1`.
-/
@[simps!]
def down (Œ± : Type*) [AddRightCancelSemigroup Œ±] [One Œ±] : ComplexShape Œ± :=
  down' 1
#align complex_shape.down ComplexShape.down
#align complex_shape.down_rel ComplexShape.down_Rel

theorem down_mk {Œ± : Type*} [AddRightCancelSemigroup Œ±] [One Œ±] (i j : Œ±) (h : j + 1 = i) :
  (down Œ±).Rel i j :=
  down'_mk (1 : Œ±) i j h
#align complex_shape.down_mk ComplexShape.down_mk

end ComplexShape
