/-
Copyright (c) 2025 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
module

public import Mathlib.Algebra.Homology.LeftResolution.Transport
public import Mathlib.CategoryTheory.Idempotents.FunctorExtension
public import Mathlib.CategoryTheory.MorphismProperty.Retract

/-!
# Left resolutions which preserve the zero object

The structure `LeftResolution` allows to define a functorial
resolution of an object (see `LeftResolution.chainComplexFunctor`
in the file `Algebra.Homology.LeftResolution.Basic`). In
order to extend this resolution to complexes, we not only
need the functoriality but also that zero morphisms
are sent to zero. In this file, given `Î¹ : C â¥¤ A`,
we extend `Î› : LeftResolution Î¹` to idempotent completions as
`Î›.karoubi : LeftResolution ((functorExtensionâ‚‚ C A).obj Î¹)`, and
when both `C` and `A` are idempotent complete, we define
`Î›.reduced : LeftResolution Î¹` in such a way that the
functor `Î›.reduced.F : A â¥¤ C` preserves zero morphisms.

For example, if `A := ModuleCat R` and `C` is the full subcategory
of flat `R`-modules, we may first define `Î›` by using the
functor which sends an `R`-module `M` to the free `R`-module
on the elements of `M`. Then, `Î›.reduced.F.obj M` will be obtained
from the free `R`-module on `M` by factoring out the direct factor
corresponding to the submodule spanned by the generator corresponding
to `0 : M` (TODO).

-/

@[expose] public section

namespace CategoryTheory.Abelian

variable {A C : Type*} [Category C] [Category A] {Î¹ : C â¥¤ A}
  (Î› : LeftResolution Î¹)

open Idempotents Limits MorphismProperty

namespace LeftResolution

variable [Preadditive C] [Preadditive A] [Î¹.Additive]

/-- Auxiliary definition for `LeftResolution.karoubi`. -/
@[simps]
def karoubi.F' : A â¥¤ Karoubi C where
  obj X := âŸ¨Î›.F.obj X, ğŸ™ _ - Î›.F.map 0, by simp [â† Functor.map_comp]âŸ©
  map {X Y} f := âŸ¨Î›.F.map f - Î›.F.map 0, by simp [â† Functor.map_comp]âŸ©
  map_comp _ _ := by simp [â† Functor.map_comp]

/-- Auxiliary definition for `LeftResolution.karoubi`. -/
@[simps!]
def karoubi.F : Karoubi A â¥¤ Karoubi C := (functorExtensionâ‚ A C).obj (karoubi.F' Î›)

instance : (karoubi.F Î›).PreservesZeroMorphisms where

/-- Auxiliary definition for `LeftResolution.karoubi`. -/
@[simps]
def karoubi.Ï€' : toKaroubi A â‹™ F Î› â‹™ (functorExtensionâ‚‚ C A).obj Î¹ âŸ¶ toKaroubi A where
  app X := âŸ¨Î›.Ï€.app X, by simpâŸ©

/-- The morphism `(karoubi.Ï€' Î›).app X` is a retract of `(toKaroubi _).map (Î›.Ï€.app X)`. -/
def karoubi.retractArrow (X : A) :
    RetractArrow ((karoubi.Ï€' Î›).app X) ((toKaroubi _).map (Î›.Ï€.app X)) where
  i := Arrow.homMk âŸ¨Î¹.map ((karoubi.F' Î›).obj X).p, by simp [â† Functor.map_comp]âŸ© (ğŸ™ _)
  r := Arrow.homMk âŸ¨Î¹.map ((karoubi.F' Î›).obj X).p, by simp [â† Functor.map_comp]âŸ© (ğŸ™ _)
  retract := by
    ext
    Â· simp [â† Functor.map_comp]
    Â· simp

instance (X : A) : Epi ((karoubi.Ï€' Î›).app X) :=
  of_retract (P := epimorphisms _) (karoubi.retractArrow Î› X)
    (epimorphisms.infer_property _)

/-- Auxiliary definition for `LeftResolution.karoubi`. -/
def karoubi.Ï€ : karoubi.F Î› â‹™ (functorExtensionâ‚‚ C A).obj Î¹ âŸ¶ ğŸ­ (Karoubi A) :=
  whiskeringLeftObjToKaroubiFullyFaithful.preimage (karoubi.Ï€' Î›)

@[simp]
lemma karoubi.Ï€_app_toKaroubi_obj (X : A) :
    (karoubi.Ï€ Î›).app ((toKaroubi _).obj X) = (karoubi.Ï€' Î›).app X := by
  simp [Ï€, whiskeringLeftObjToKaroubiFullyFaithful]

instance (X : A) : Epi ((karoubi.Ï€ Î›).app ((toKaroubi _).obj X)) := by
  rw [karoubi.Ï€_app_toKaroubi_obj]
  infer_instance

instance (X : Karoubi A) : Epi ((karoubi.Ï€ Î›).app X) :=
  of_retract (P := epimorphisms _) (NatTrans.retractArrowApp (karoubi.Ï€ Î›) X.retract)
    (epimorphisms.infer_property _)

/-- Given `Î¹ : C â¥¤ A`, this is the extension of `Î› : LeftResolution Î¹` to
`LeftResolution ((functorExtensionâ‚‚ C A).obj Î¹)`, where
`(functorExtensionâ‚‚ C A).obj Î¹ : Karoubi C â¥¤ Karoubi A` is the extension of `Î¹`. -/
@[simps]
noncomputable def karoubi : LeftResolution ((functorExtensionâ‚‚ C A).obj Î¹) where
  F := karoubi.F Î›
  Ï€ := karoubi.Ï€ Î›

instance : Î›.karoubi.F.PreservesZeroMorphisms where

section

variable [IsIdempotentComplete A] [IsIdempotentComplete C]

/-- Given an additive functor `Î¹ : C â¥¤ A` between idempotent complete categories,
any `Î› : LeftResolution Î¹` induces a term `Î›.reduced : LeftResolution Î¹`
such that `Î›.reduced.F` preserves zero morphisms. -/
noncomputable def reduced : LeftResolution Î¹ :=
  Î›.karoubi.transport (toKaroubiEquivalence A) (toKaroubiEquivalence C)
     ((karoubiUniversalâ‚ C A).unitIso.app _)

instance : Î›.reduced.F.PreservesZeroMorphisms := by
  dsimp [reduced, transport]
  infer_instance

end

end LeftResolution

end CategoryTheory.Abelian
