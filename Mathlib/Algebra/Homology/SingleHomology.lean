/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Homology.Single
import Mathlib.Algebra.Homology.ShortComplex.HomologicalComplex
/-!
# The homology of single complexes

The main definition in this file is `HomologicalComplex.homologyFunctorSingleIso`
which is a natural isomorphism `single C c j ‚ãô homologyFunctor C c j ‚âÖ ùü≠ C`.

-/

universe v u

open CategoryTheory Category Limits ZeroObject

variable {C : Type u} [Category.{v} C] [HasZeroMorphisms C] [HasZeroObject C]
  {Œπ : Type*} [DecidableEq Œπ] (c : ComplexShape Œπ) (j : Œπ)

namespace HomologicalComplex

variable (A : C)

instance (i : Œπ) : ((single C c j).obj A).HasHomology i := by
  apply ShortComplex.hasHomology_of_zeros

lemma exactAt_single_obj (A : C) (i : Œπ) (hi : i ‚â† j) :
    ExactAt ((single C c j).obj A) i :=
  ShortComplex.exact_of_isZero_X‚ÇÇ _ (isZero_single_obj_X c _ _ _ hi)

lemma isZero_single_obj_homology (A : C) (i : Œπ) (hi : i ‚â† j) :
    IsZero (((single C c j).obj A).homology i) := by
  simpa only [‚Üê exactAt_iff_isZero_homology]
    using exactAt_single_obj c j A i hi

/-- The canonical isomorphism `((single C c j).obj A).cycles j ‚âÖ A` -/
noncomputable def singleObjCyclesSelfIso :
    ((single C c j).obj A).cycles j ‚âÖ A :=
  ((single C c j).obj A).iCyclesIso j _ rfl rfl ‚â™‚â´ singleObjXSelf c j A

@[reassoc]
lemma singleObjCyclesSelfIso_hom :
    (singleObjCyclesSelfIso c j A).hom =
      ((single C c j).obj A).iCycles j ‚â´ (singleObjXSelf c j A).hom := rfl

/-- The canonical isomorphism `((single C c j).obj A).opcycles j ‚âÖ A` -/
noncomputable def singleObjOpcyclesSelfIso :
    A ‚âÖ ((single C c j).obj A).opcycles j :=
  (singleObjXSelf c j A).symm ‚â™‚â´ ((single C c j).obj A).pOpcyclesIso _ j rfl rfl

@[reassoc]
lemma singleObjOpcyclesSelfIso_hom :
    (singleObjOpcyclesSelfIso c j A).hom =
      (singleObjXSelf c j A).inv ‚â´ ((single C c j).obj A).pOpcycles j := rfl

/-- The canonical isomorphism `((single C c j).obj A).homology j ‚âÖ A` -/
noncomputable def singleObjHomologySelfIso :
    ((single C c j).obj A).homology j ‚âÖ A :=
  (((single C c j).obj A).isoHomologyœÄ _ j rfl rfl).symm ‚â™‚â´ singleObjCyclesSelfIso c j A

@[reassoc (attr := simp)]
lemma singleObjCyclesSelfIso_inv_iCycles :
    (singleObjCyclesSelfIso _ _ _).inv ‚â´ ((single C c j).obj A).iCycles j =
      (singleObjXSelf c j A).inv := by
  simp [singleObjCyclesSelfIso]

@[reassoc (attr := simp)]
lemma homologyœÄ_singleObjHomologySelfIso_hom :
    ((single C c j).obj A).homologyœÄ j ‚â´ (singleObjHomologySelfIso _ _ _).hom =
      (singleObjCyclesSelfIso _ _ _).hom := by
  simp [singleObjCyclesSelfIso, singleObjHomologySelfIso]

@[reassoc (attr := simp)]
lemma singleObjHomologySelfIso_hom_singleObjHomologySelfIso_inv :
    (singleObjCyclesSelfIso c j A).hom ‚â´ (singleObjHomologySelfIso c j A).inv =
      ((single C c j).obj A).homologyœÄ j := by
  simp only [‚Üê cancel_mono (singleObjHomologySelfIso _ _ _).hom, assoc,
    Iso.inv_hom_id, comp_id, homologyœÄ_singleObjHomologySelfIso_hom]

@[reassoc (attr := simp)]
lemma singleObjCyclesSelfIso_hom_singleObjOpcyclesSelfIso_hom :
    (singleObjCyclesSelfIso c j A).hom ‚â´ (singleObjOpcyclesSelfIso c j A).hom =
      ((single C c j).obj A).iCycles j ‚â´ ((single C c j).obj A).pOpcycles j := by
  simp [singleObjCyclesSelfIso, singleObjOpcyclesSelfIso]

@[reassoc (attr := simp)]
lemma singleObjCyclesSelfIso_inv_homologyœÄ :
    (singleObjCyclesSelfIso _ _ _).inv ‚â´ ((single C c j).obj A).homologyœÄ j =
      (singleObjHomologySelfIso _ _ _).inv := by
  simp [singleObjCyclesSelfIso, singleObjHomologySelfIso]

@[reassoc (attr := simp)]
lemma singleObjHomologySelfIso_inv_homologyŒπ :
    (singleObjHomologySelfIso _ _ _).inv ‚â´ ((single C c j).obj A).homologyŒπ j =
      (singleObjOpcyclesSelfIso _ _ _).hom := by
  rw [‚Üê cancel_epi (singleObjCyclesSelfIso c j A).hom,
    singleObjHomologySelfIso_hom_singleObjHomologySelfIso_inv_assoc, homology_œÄ_Œπ,
    singleObjCyclesSelfIso_hom_singleObjOpcyclesSelfIso_hom]

@[reassoc (attr := simp)]
lemma homologyŒπ_singleObjOpcyclesSelfIso_inv :
    ((single C c j).obj A).homologyŒπ j ‚â´ (singleObjOpcyclesSelfIso _ _ _).inv =
      (singleObjHomologySelfIso _ _ _).hom := by
  rw [‚Üê cancel_epi (singleObjHomologySelfIso _ _ _).inv,
    singleObjHomologySelfIso_inv_homologyŒπ_assoc, Iso.hom_inv_id, Iso.inv_hom_id]

@[reassoc (attr := simp)]
lemma singleObjHomologySelfIso_hom_singleObjOpcyclesSelfIso_hom :
    (singleObjHomologySelfIso _ _ _).hom ‚â´ (singleObjOpcyclesSelfIso _ _ _).hom =
      ((single C c j).obj A).homologyŒπ j := by
  rw [‚Üê cancel_epi (singleObjHomologySelfIso _ _ _).inv,
    Iso.inv_hom_id_assoc, singleObjHomologySelfIso_inv_homologyŒπ]

@[reassoc (attr := simp)]
lemma pOpcycles_singleObjOpcyclesSelfIso_inv :
    ((single C c j).obj A).pOpcycles j ‚â´ (singleObjOpcyclesSelfIso _ _ _).inv =
      (singleObjXSelf c j A).hom := by
  have := ((single C c j).obj A).isIso_iCycles j _ rfl (by simp)
  rw [‚Üê cancel_epi (((single C c j).obj A).iCycles j),
    ‚Üê HomologicalComplex.homology_œÄ_Œπ_assoc, homologyŒπ_singleObjOpcyclesSelfIso_inv,
    homologyœÄ_singleObjHomologySelfIso_hom, singleObjCyclesSelfIso_hom]

variable {A}
variable {B : C} (f : A ‚ü∂ B)

@[reassoc (attr := simp)]
lemma singleObjCyclesSelfIso_hom_naturality :
    cyclesMap ((single C c j).map f) j ‚â´ (singleObjCyclesSelfIso c j B).hom =
      (singleObjCyclesSelfIso c j A).hom ‚â´ f := by
  rw [‚Üê cancel_mono (singleObjCyclesSelfIso c j B).inv, assoc, assoc, Iso.hom_inv_id, comp_id,
    ‚Üê cancel_mono (iCycles _ _)]
  simp only [cyclesMap_i, singleObjCyclesSelfIso, Iso.trans_hom, iCyclesIso_hom, Iso.trans_inv,
    assoc, iCyclesIso_inv_hom_id, comp_id, single_map_f_self]

@[reassoc (attr := simp)]
lemma singleObjCyclesSelfIso_inv_naturality :
    (singleObjCyclesSelfIso c j A).inv ‚â´ cyclesMap ((single C c j).map f) j =
      f ‚â´ (singleObjCyclesSelfIso c j B).inv := by
  rw [‚Üê cancel_epi (singleObjCyclesSelfIso c j A).hom, Iso.hom_inv_id_assoc,
    ‚Üê singleObjCyclesSelfIso_hom_naturality_assoc, Iso.hom_inv_id, comp_id]

@[reassoc (attr := simp)]
lemma singleObjHomologySelfIso_hom_naturality :
    homologyMap ((single C c j).map f) j ‚â´ (singleObjHomologySelfIso c j B).hom =
      (singleObjHomologySelfIso c j A).hom ‚â´ f := by
  rw [‚Üê cancel_epi (((single C c j).obj A).homologyœÄ j),
    homologyœÄ_naturality_assoc, homologyœÄ_singleObjHomologySelfIso_hom,
    singleObjCyclesSelfIso_hom_naturality, homologyœÄ_singleObjHomologySelfIso_hom_assoc]

@[reassoc (attr := simp)]
lemma singleObjHomologySelfIso_inv_naturality :
    (singleObjHomologySelfIso c j A).inv ‚â´ homologyMap ((single C c j).map f) j =
      f ‚â´ (singleObjHomologySelfIso c j B).inv := by
  rw [‚Üê cancel_mono (singleObjHomologySelfIso c j B).hom, assoc, assoc,
    singleObjHomologySelfIso_hom_naturality,
    Iso.inv_hom_id_assoc, Iso.inv_hom_id, comp_id]

@[reassoc (attr := simp)]
lemma singleObjOpcyclesSelfIso_hom_naturality :
    (singleObjOpcyclesSelfIso c j A).hom ‚â´ opcyclesMap ((single C c j).map f) j  =
      f ‚â´ (singleObjOpcyclesSelfIso c j B).hom := by
  rw [‚Üê cancel_epi (singleObjCyclesSelfIso c j A).hom,
    singleObjCyclesSelfIso_hom_singleObjOpcyclesSelfIso_hom_assoc, p_opcyclesMap,
    single_map_f_self, assoc, assoc, singleObjCyclesSelfIso_hom,
    singleObjOpcyclesSelfIso_hom, assoc]

@[reassoc (attr := simp)]
lemma singleObjOpcyclesSelfIso_inv_naturality :
    opcyclesMap ((single C c j).map f) j ‚â´ (singleObjOpcyclesSelfIso c j B).inv =
      (singleObjOpcyclesSelfIso c j A).inv ‚â´ f := by
  rw [‚Üê cancel_mono (singleObjOpcyclesSelfIso c j B).hom, assoc, assoc, Iso.inv_hom_id,
    comp_id, ‚Üê singleObjOpcyclesSelfIso_hom_naturality, Iso.inv_hom_id_assoc]

variable (C)

/-- The computation of the homology of single complexes, as a natural isomorphism
`single C c j ‚ãô homologyFunctor C c j ‚âÖ ùü≠ C`. -/
@[simps!]
noncomputable def homologyFunctorSingleIso [CategoryWithHomology C] :
    single C c j ‚ãô homologyFunctor C c j ‚âÖ ùü≠ _ :=
  NatIso.ofComponents (fun A ‚Ü¶ (singleObjHomologySelfIso c j A))
    (fun f ‚Ü¶ singleObjHomologySelfIso_hom_naturality c j f)

end HomologicalComplex

open HomologicalComplex

lemma ChainComplex.exactAt_succ_single_obj (A : C) (n : ‚Ñï) :
    ExactAt ((single‚ÇÄ C).obj A) (n + 1) :=
  exactAt_single_obj _ _ _ _ (by simp)

lemma CochainComplex.exactAt_succ_single_obj (A : C) (n : ‚Ñï) :
    ExactAt ((single‚ÇÄ C).obj A) (n + 1) :=
  exactAt_single_obj _ _ _ _ (by simp)
