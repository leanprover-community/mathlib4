import Mathlib.Algebra.Homology.Homotopy

open CategoryTheory Limits HomologicalComplex Category
  ZeroObject

namespace HomologicalComplex

variable {C Î¹ : Type*} [Category C] [HasZeroMorphisms C]
  [HasZeroObject C] {c : ComplexShape Î¹} [DecidableEq Î¹] [DecidableRel c.Rel]
  (K : HomologicalComplex C c)

noncomputable def prevX (j : Î¹) : C :=
  if c.Rel (c.prev j) j
  then K.X (c.prev j)
  else 0

lemma isZero_prevX (j : Î¹) (h : Â¬ c.Rel (c.prev j) j) :
    IsZero (K.prevX j) := by
  dsimp [prevX]
  rw [if_neg h]
  exact Limits.isZero_zero C

noncomputable def prevXIso (i j : Î¹) (hij : c.Rel i j) : K.prevX j â‰… K.X i :=
  eqToIso (by
    dsimp [prevX]
    obtain rfl := c.prev_eq' hij
    rw [if_pos hij])

noncomputable def prevXd (j : Î¹) : K.prevX j âŸ¶ K.X j :=
  if hj : c.Rel (c.prev j) j
  then (K.prevXIso _ _ hj).hom â‰« K.d (c.prev j) j
  else 0

@[reassoc (attr := simp)]
lemma prevXIso_inv_d {i j : Î¹} (hij : c.Rel i j) :
    (K.prevXIso _ _ hij).inv â‰« K.prevXd j = K.d i j := by
  obtain rfl := c.prev_eq' hij
  dsimp [prevXd]
  rw [dif_pos hij, Iso.inv_hom_id_assoc]

@[reassoc (attr := simp)]
lemma prevXd_d (j k : Î¹) : K.prevXd j â‰« K.d j k = 0 := by
  dsimp [prevXd]
  split_ifs <;> simp

end HomologicalComplex

namespace Homotopy

variable {C Î¹ : Type*} [Category C] [Preadditive C]
  [HasZeroObject C] {c : ComplexShape Î¹} [DecidableEq Î¹] [DecidableRel c.Rel]
  {K L : HomologicalComplex C c} {f g : K âŸ¶ L} (H : Homotopy f g)

noncomputable def homToPrevX (j : Î¹) : K.X j âŸ¶ L.prevX j :=
  if hj : c.Rel (c.prev j) j
  then H.hom _ _ â‰« (L.prevXIso _ _ hj).inv
  else 0

@[reassoc]
lemma homToPrevX_prevXd (j : Î¹) :
    H.homToPrevX j â‰« L.prevXd j = prevD j H.hom := by
  dsimp [prevXd, homToPrevX]
  split_ifs with h
  Â· simp [toPrev, dTo]
  Â· dsimp [dTo]
    rw [L.shape _ _ h, comp_zero, comp_zero]

@[reassoc (attr := simp)]
lemma homToPrevX_prevXIso_hom (i j : Î¹) (hij : c.Rel i j) :
    H.homToPrevX j â‰« (L.prevXIso _ _ hij).hom = H.hom j i := by
  obtain rfl := c.prev_eq' hij
  simp [homToPrevX, dif_pos hij]

end Homotopy

namespace CochainComplex

variable {C : Type*} [Category C] [Preadditive C] [HasZeroObject C]
  {Î± : Type*} [AddRightCancelSemigroup Î±] [One Î±] [DecidableEq Î±]

section

variable {F G : CochainComplex C Î±} (f : F âŸ¶ G)
  [âˆ€ (n : Î±), HasBinaryBiproduct (F.X n) (G.prevX n)]

noncomputable def homotopyFiber : CochainComplex C Î± where
  X i := F.X i âŠ G.prevX i
  d i j :=
    if hij : i + 1 = j
    then
      biprod.fst â‰« F.d _ _ â‰« biprod.inl -
      (biprod.fst â‰« f.f i + biprod.snd â‰« G.prevXd i) â‰« (G.prevXIso _ _ hij).inv â‰« biprod.inr
    else 0
  shape i j (hij : i + 1 â‰  j) := by simp only [dif_neg hij]
  d_comp_d' := by
    rintro i _ _ rfl rfl
    simp

namespace HomotopyFiber

lemma d_eq (i j : Î±) (h : i + 1 = j) :
    (homotopyFiber f).d i j =
      biprod.fst â‰« F.d _ _ â‰« biprod.inl -
      (biprod.fst â‰« f.f i + biprod.snd â‰« G.prevXd i) â‰« (G.prevXIso _ _ h).inv â‰« biprod.inr :=
  dif_pos h

noncomputable def fst : homotopyFiber f âŸ¶ F where
  f n := biprod.fst
  comm' := by
    rintro i _ rfl
    dsimp
    simp only [d_eq _ _ _ rfl, Preadditive.add_comp, assoc, Preadditive.sub_comp,
      biprod.inl_fst, comp_id, biprod.inr_fst, comp_zero, add_zero, sub_zero]

noncomputable def lift {M : CochainComplex C Î±} (Ï† : M âŸ¶ F) (h : Homotopy (Ï† â‰« f) 0) :
    M âŸ¶ homotopyFiber f where
  f n := biprod.lift (Ï†.f n) (-h.homToPrevX n)
  comm' := by
    rintro i _ rfl
    rw [d_eq _ _ _ rfl]
    simp only [Preadditive.add_comp, assoc, Preadditive.comp_sub,
      biprod.lift_fst_assoc, Hom.comm_assoc, Preadditive.comp_add, biprod.lift_snd_assoc,
      Preadditive.neg_comp]
    apply biprod.hom_ext
    Â· simp
    Â· dsimp
      simp only [ComplexShape.up_Rel, not_true, Preadditive.sub_comp, Preadditive.neg_comp,
        biprod.inl_snd, comp_zero, neg_zero, biprod.inr_snd,
        comp_id, zero_sub, neg_add_rev, neg_neg, biprod.lift_snd,
        â† cancel_mono (G.prevXIso i (i + 1) rfl).hom,
        Preadditive.add_comp, assoc, Iso.inv_hom_id, comp_id, zero_f,
        h.homToPrevX_prevXd, h.homToPrevX_prevXIso_hom, â† comp_f, h.comm i, add_zero,
        dNext_eq h.hom rfl, Preadditive.comp_neg, add_neg_cancel_left]

@[reassoc (attr := simp)]
lemma lift_fst {M : CochainComplex C Î±} (Ï† : M âŸ¶ F) (h : Homotopy (Ï† â‰« f) 0) :
    lift f Ï† h â‰« fst f = Ï† := by
  ext n
  simp [lift, fst]

end HomotopyFiber

end

section

variable (K L : CochainComplex C Î±) [DecidableEq Î±] [HasBinaryBiproduct K K]
  [âˆ€ (n : Î±), HasBinaryBiproduct ((K âŠ K).X n) (prevX K n)]

noncomputable abbrev pathObject : CochainComplex C Î± := homotopyFiber (biprod.desc (ğŸ™ K) (-ğŸ™ K))

namespace pathObject

variable {K L}

noncomputable def Ï€â‚€ : pathObject K âŸ¶ K := HomotopyFiber.fst _ â‰« biprod.fst

noncomputable def Ï€â‚ : pathObject K âŸ¶ K := HomotopyFiber.fst _ â‰« biprod.snd

section

variable (fâ‚€ fâ‚ : L âŸ¶ K) (h : Homotopy fâ‚€ fâ‚)

noncomputable def lift : L âŸ¶ pathObject K :=
  HomotopyFiber.lift _ (biprod.lift fâ‚€ fâ‚)
    (Homotopy.trans (Homotopy.ofEq (by simp [sub_eq_add_neg])) (Homotopy.equivSubZero h))

@[reassoc (attr := simp)]
lemma lift_Ï€â‚€ : lift fâ‚€ fâ‚ h â‰« Ï€â‚€ = fâ‚€ := by simp [lift, Ï€â‚€]

@[reassoc (attr := simp)]
lemma lift_Ï€â‚ : lift fâ‚€ fâ‚ h â‰« Ï€â‚ = fâ‚ := by simp [lift, Ï€â‚]

end

noncomputable def Î¹ : K âŸ¶ pathObject K := lift (ğŸ™ K) (ğŸ™ K) (Homotopy.refl _)

@[reassoc (attr := simp)]
lemma Î¹_Ï€â‚€ : Î¹ â‰« Ï€â‚€ = ğŸ™ K := lift_Ï€â‚€ _ _ _

@[reassoc (attr := simp)]
lemma Î¹_Ï€â‚ : Î¹ â‰« Ï€â‚ = ğŸ™ K := lift_Ï€â‚ _ _ _

end pathObject

end

end CochainComplex
