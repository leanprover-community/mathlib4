/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Algebra.Homology.ShortComplex.HomologicalComplex
import Mathlib.Algebra.Module.BigOperators
import Mathlib.AlgebraicTopology.ExtraDegeneracy

/-!
# The alternating constant complex

Given an object `X : C` and endomorphisms `Ï†, Ïˆ : X âŸ¶ X` such that `Ï† âˆ˜ Ïˆ = Ïˆ âˆ˜ Ï† = 0`, this file
defines the periodic chain and cochain complexes
`... âŸ¶ X --Ï†--> X --Ïˆ--> X --Ï†--> X --Ïˆ--> 0` and `0 âŸ¶ X --Ïˆ--> X --Ï†--> X --Ïˆ--> X --Ï†--> ...`
(or more generally for any complex shape `c` on `â„•` where `c.Rel i j` implies `i` and `j` have
different parity). We calculate the homology of these periodic complexes.

In particular, we show `... âŸ¶ X --ðŸ™--> X --0--> X --ðŸ™--> X --0--> X âŸ¶ 0` is homotopy equivalent
to the single complex where `X` is in degree `0`.

-/
universe v u

open CategoryTheory Limits

namespace ComplexShape

lemma up_nat_odd_add {i j : â„•} (h : (ComplexShape.up â„•).Rel i j) : Odd (i + j) := by
  subst h
  norm_num

lemma down_nat_odd_add {i j : â„•} (h : (ComplexShape.down â„•).Rel i j) : Odd (i + j) := by
  subst h
  norm_num

end ComplexShape

namespace HomologicalComplex

open ShortComplex

variable {C : Type*} [Category C] [Limits.HasZeroMorphisms C]
  (A : C) {Ï† : A âŸ¶ A} {Ïˆ : A âŸ¶ A} (hOdd : Ï† â‰« Ïˆ = 0) (hEven : Ïˆ â‰« Ï† = 0)

/-- Let `c : ComplexShape â„•` be such that `i j : â„•` have opposite parity if they are related by
`c`. Let `Ï†, Ïˆ : A âŸ¶ A` be such that `Ï† âˆ˜ Ïˆ = Ïˆ âˆ˜ Ï† = 0`. This is a complex of shape `c` whose
objects are all `A`. For all `i, j` related by `c`, `dáµ¢â±¼ = Ï†` when `i` is even, and `dáµ¢â±¼ = Ïˆ` when
`i` is odd. -/
@[simps!]
noncomputable def alternatingConst {c : ComplexShape â„•} [DecidableRel c.Rel]
    (hc : âˆ€ i j, c.Rel i j â†’ Odd (i + j)) :
    HomologicalComplex C c where
  X n := A
  d i j :=
    if hij : c.Rel i j then
      if hi : Even i then Ï†
      else Ïˆ
    else 0
  shape i j := by aesop
  d_comp_d' i j k hij hjk := by
    have := hc i j hij
    split_ifs with hi hj hj
    Â· exact False.elim <| Nat.not_odd_iff_even.2 hi <| by simp_all [Nat.odd_add]
    Â· assumption
    Â· assumption
    Â· exact False.elim <| hj <| by simp_all [Nat.odd_add]

variable {c : ComplexShape â„•} [DecidableRel c.Rel] (hc : âˆ€ i j, c.Rel i j â†’ Odd (i + j))

open HomologicalComplex hiding mk

/-- The `i, j, k`th short complex associated to the alternating constant complex on `Ï†, Ïˆ : A âŸ¶ A`
is `A --Ïˆ--> A --Ï†--> A` when `i ~ j, j ~ k` and `j` is even. -/
noncomputable def alternatingConstScIsoEven
    {i j k : â„•} (hij : c.Rel i j) (hjk : c.Rel j k) (h : Even j) :
    (alternatingConst A hOdd hEven hc).sc' i j k â‰… ShortComplex.mk Ïˆ Ï† hEven :=
  isoMk (Iso.refl _) (Iso.refl _) (Iso.refl _)
    (by
      simp_all only [alternatingConst, dite_eq_ite, Iso.refl_hom, Category.id_comp,
        shortComplexFunctor'_obj_f, â†“reduceIte, Category.comp_id, right_eq_ite_iff]
      intro hi
      have := hc i j hij
      exact False.elim <| Nat.not_odd_iff_even.2 hi <| by simp_all [Nat.odd_add])
    (by simp_all [alternatingConst])

/-- The `i, j, k`th short complex associated to the alternating constant complex on `Ï†, Ïˆ : A âŸ¶ A`
is `A --Ï†--> A --Ïˆ--> A` when `i ~ j, j ~ k` and `j` is even. -/
noncomputable def alternatingConstScIsoOdd
    {i j k : â„•} (hij : c.Rel i j) (hjk : c.Rel j k) (h : Odd j) :
    (alternatingConst A hOdd hEven hc).sc' i j k â‰… ShortComplex.mk Ï† Ïˆ hOdd :=
  isoMk (Iso.refl _) (Iso.refl _) (Iso.refl _)
    (by
      simp_all only [alternatingConst, dite_eq_ite, Iso.refl_hom, Category.id_comp,
        shortComplexFunctor'_obj_f, â†“reduceIte, Category.comp_id, left_eq_ite_iff]
      intro hi
      have := hc i j hij
      exact False.elim <| Nat.not_even_iff_odd.2 h <| by simp_all [Nat.odd_add])
    (by simp_all [alternatingConst])

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma alternatingConst_iCycles_even_comp [CategoryWithHomology C]
    {j : â„•} (hpj : c.Rel (c.prev j) j) (hnj : c.Rel j (c.next j)) (h : Even j) :
    (alternatingConst A hOdd hEven hc).iCycles j â‰« Ï† = 0 := by
  rw [â† cancel_epi (ShortComplex.cyclesMapIso
    (alternatingConstScIsoEven A hOdd hEven hc hpj hnj h)).inv]
  simpa [HomologicalComplex.iCycles, -Preadditive.IsIso.comp_left_eq_zero, HomologicalComplex.sc,
    HomologicalComplex.shortComplexFunctor, alternatingConstScIsoEven,
    Category.id_comp (X := (alternatingConst A hOdd hEven hc).X _)]
    using (ShortComplex.mk Ïˆ Ï† hEven).iCycles_g

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma alternatingConst_iCycles_odd_comp [CategoryWithHomology C]
    {j : â„•} (hpj : c.Rel (c.prev j) j) (hnj : c.Rel j (c.next j)) (h : Odd j) :
    (alternatingConst A hOdd hEven hc).iCycles j â‰« Ïˆ = 0 := by
  rw [â† cancel_epi (ShortComplex.cyclesMapIso
    (alternatingConstScIsoOdd A hOdd hEven hc hpj hnj h)).inv]
  simpa [HomologicalComplex.iCycles, -Preadditive.IsIso.comp_left_eq_zero, HomologicalComplex.sc,
    HomologicalComplex.shortComplexFunctor, alternatingConstScIsoOdd,
    Category.id_comp (X := (alternatingConst A hOdd hEven hc).X _)]
    using (ShortComplex.mk Ï† Ïˆ hOdd).iCycles_g

/-- The `j`th homology of the alternating constant complex on `Ï†, Ïˆ : A âŸ¶ A` is the homology of
`A --Ïˆ--> A --Ï†--> A` when `prev(j) ~ j, j ~ next(j)` and `j` is even. -/
noncomputable def alternatingConstHomologyIsoEven [CategoryWithHomology C]
    {j : â„•} (hpj : c.Rel (c.prev j) j) (hnj : c.Rel j (c.next j)) (h : Even j) :
    (alternatingConst A hOdd hEven hc).homology j â‰… (ShortComplex.mk Ïˆ Ï† hEven).homology :=
  ShortComplex.homologyMapIso (alternatingConstScIsoEven A hOdd hEven hc hpj hnj h)

/-- The `j`th homology of the alternating constant complex on `Ï†, Ïˆ : A âŸ¶ A` is the homology of
`A --Ï†--> A --Ïˆ--> A` when `prev(j) ~ j, j ~ next(j)` and `j` is odd. -/
noncomputable def alternatingConstHomologyIsoOdd [CategoryWithHomology C]
    {j : â„•} (hpj : c.Rel (c.prev j) j) (hnj : c.Rel j (c.next j)) (h : Odd j) :
    (alternatingConst A hOdd hEven hc).homology j â‰… (ShortComplex.mk Ï† Ïˆ hOdd).homology :=
  ShortComplex.homologyMapIso (alternatingConstScIsoOdd A hOdd hEven hc hpj hnj h)

end HomologicalComplex

open CategoryTheory Limits AlgebraicTopology

variable {C : Type*} [Category C]

namespace ChainComplex

/-- The chain complex `X â†0- X â†ðŸ™- X â†0- X â†ðŸ™- X â‹¯`.
It is exact away from `0` and has homology `X` at `0`. -/
@[simps]
noncomputable def alternatingConst [HasZeroMorphisms C] : C â¥¤ ChainComplex C â„• where
  obj X := HomologicalComplex.alternatingConst X (Category.id_comp 0) (Category.comp_id 0)
    (fun _ _ => ComplexShape.down_nat_odd_add)
  map {X Y} f := {
    f _ := f
    comm' i j hij := by by_cases Even i <;> simp_all [-Nat.not_even_iff_odd] }

variable [HasZeroMorphisms C] [HasZeroObject C]

open ZeroObject

/-- The `n`-th homology of the alternating constant complex is zero for non-zero even `n`. -/
noncomputable
def alternatingConstHomologyDataEvenNEZero (X : C) (n : â„•) (hn : Even n) (hâ‚€ : n â‰  0) :
    ((alternatingConst.obj X).sc n).HomologyData :=
  .ofIsLimitKernelFork _ (by simp [Nat.even_add_one, hn]) _
    (Limits.zeroKernelOfCancelZero _ (by cases n <;> simp_all))

/-- The `n`-th homology of the alternating constant complex is zero for odd `n`. -/
noncomputable
def alternatingConstHomologyDataOdd (X : C) (n : â„•) (hn : Odd n) :
    ((alternatingConst.obj X).sc n).HomologyData :=
  .ofIsColimitCokernelCofork _ (by simp [hn]) _ (Limits.zeroCokernelOfZeroCancel _ (by simp [hn]))

/-- The `n`-th homology of the alternating constant complex is `X` for `n = 0`. -/
noncomputable
def alternatingConstHomologyDataZero (X : C) (n : â„•) (hn : n = 0) :
    ((alternatingConst.obj X).sc n).HomologyData :=
  .ofZeros _ (by simp [hn]) (by simp [hn])

instance (X : C) (n : â„•) : (alternatingConst.obj X).HasHomology n := by
  rcases n.even_or_odd with h | h
  Â· rcases n with - | n
    Â· exact âŸ¨âŸ¨alternatingConstHomologyDataZero X _ rflâŸ©âŸ©
    Â· exact âŸ¨âŸ¨alternatingConstHomologyDataEvenNEZero X _ h (by simp)âŸ©âŸ©
  Â· exact âŸ¨âŸ¨alternatingConstHomologyDataOdd X _ hâŸ©âŸ©

/-- The `n`-th homology of the alternating constant complex is `X` for `n â‰  0`. -/
lemma alternatingConst_exactAt (X : C) (n : â„•) (hn : n â‰  0) :
    (alternatingConst.obj X).ExactAt n := by
  rcases n.even_or_odd with h | h
  Â· exact âŸ¨(alternatingConstHomologyDataEvenNEZero X _ h hn), isZero_zero CâŸ©
  Â· exact âŸ¨(alternatingConstHomologyDataOdd X _ h), isZero_zero CâŸ©

/-- The `n`-th homology of the alternating constant complex is `X` for `n = 0`. -/
noncomputable
def alternatingConstHomologyZero (X : C) : (alternatingConst.obj X).homology 0 â‰… X :=
  (alternatingConstHomologyDataZero X _ rfl).left.homologyIso

end ChainComplex

variable [Preadditive C] [HasZeroObject C]

/-- The alternating face complex of the constant complex is the alternating constant complex. -/
noncomputable def AlgebraicTopology.alternatingFaceMapComplexConst :
    Functor.const _ â‹™ alternatingFaceMapComplex C â‰… ChainComplex.alternatingConst :=
  NatIso.ofComponents (fun X â†¦ HomologicalComplex.Hom.isoOfComponents (fun _ â†¦ Iso.refl _) <| by
    rintro _ i rfl
    simp [SimplicialObject.Î´, â† Finset.sum_smul, Fin.sum_neg_one_pow, Nat.even_add_one,
      -Nat.not_even_iff_odd]) (by intros; ext; simp)

namespace ChainComplex

/-- `alternatingConst.obj X` is homotopy equivalent to the chain
complex `(singleâ‚€ C).obj X`. -/
noncomputable def alternatingConstHomotopyEquiv (X : C) :
    HomotopyEquiv (alternatingConst.obj X) ((singleâ‚€ C).obj X) :=
  (HomotopyEquiv.ofIso (alternatingFaceMapComplexConst.app X).symm).trans
    ((SimplicialObject.Augmented.ExtraDegeneracy.const X).homotopyEquiv)

end ChainComplex
