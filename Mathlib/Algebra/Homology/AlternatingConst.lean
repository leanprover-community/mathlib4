/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Algebra.Homology.ShortComplex.HomologicalComplex
import Mathlib.Algebra.Module.BigOperators
import Mathlib.AlgebraicTopology.ExtraDegeneracy

/-!
# The alternating constant complex

In this file we define the chain complex `X ‚Üê0- X ‚Üêùüô- X ‚Üê0- X ‚Üêùüô- X ‚ãØ`,
calculate its homology, and show that it is homotopy equivalent
to the single complex where `X` is in degree `0`.

-/

open CategoryTheory Limits AlgebraicTopology

variable {C : Type*} [Category C]

namespace ChainComplex

/-- The chain complex `X ‚Üê0- X ‚Üêùüô- X ‚Üê0- X ‚Üêùüô- X ‚ãØ`.
It is exact away from `0` and has homology `X` at `0`. -/
@[simps]
def alternatingConst [HasZeroMorphisms C] : C ‚•§ ChainComplex C ‚Ñï where
  obj X :=
  { X _ := X
    d i j := if Even i ‚àß j + 1 = i then ùüô X else 0
    shape := by simp +contextual
    d_comp_d' := by
      simp only [ComplexShape.down_Rel]
      rintro _ _ i rfl rfl
      by_cases h : Even i <;> simp [Nat.even_add_one, ‚Üê Nat.not_even_iff_odd, h] }
  map {X Y} f := { f _ := f }

variable [HasZeroMorphisms C] [HasZeroObject C]

open ZeroObject

/-- The `n`-th homology of the alternating constant complex is zero for non-zero even `n`. -/
noncomputable
def alternatingConstHomologyDataEvenNEZero (X : C) (n : ‚Ñï) (hn : Even n) (h‚ÇÄ : n ‚â† 0) :
    ((alternatingConst.obj X).sc n).HomologyData :=
  .ofIsLimitKernelFork _ (by simp [Nat.even_add_one, hn]) _
    (Limits.zeroKernelOfCancelZero _ (by cases n <;> simp_all))

/-- The `n`-th homology of the alternating constant complex is zero for odd `n`. -/
noncomputable
def alternatingConstHomologyDataOdd (X : C) (n : ‚Ñï) (hn : Odd n) :
    ((alternatingConst.obj X).sc n).HomologyData :=
  .ofIsColimitCokernelCofork _ (by simp [hn]) _ (Limits.zeroCokernelOfZeroCancel _ (by simp [hn]))

/-- The `n`-th homology of the alternating constant complex is `X` for `n = 0`. -/
noncomputable
def alternatingConstHomologyDataZero (X : C) (n : ‚Ñï) (hn : n = 0) :
    ((alternatingConst.obj X).sc n).HomologyData :=
  .ofZeros _ (by simp [hn]) (by simp [hn])

instance (X : C) (n : ‚Ñï) : (alternatingConst.obj X).HasHomology n := by
  rcases n.even_or_odd with h | h
  ¬∑ rcases n with - | n
    ¬∑ exact ‚ü®‚ü®alternatingConstHomologyDataZero X _ rfl‚ü©‚ü©
    ¬∑ exact ‚ü®‚ü®alternatingConstHomologyDataEvenNEZero X _ h (by simp)‚ü©‚ü©
  ¬∑ exact ‚ü®‚ü®alternatingConstHomologyDataOdd X _ h‚ü©‚ü©

/-- The `n`-th homology of the alternating constant complex is `X` for `n ‚â† 0`. -/
lemma alternatingConst_exactAt (X : C) (n : ‚Ñï) (hn : n ‚â† 0) :
    (alternatingConst.obj X).ExactAt n := by
  rcases n.even_or_odd with h | h
  ¬∑ exact ‚ü®(alternatingConstHomologyDataEvenNEZero X _ h hn), isZero_zero C‚ü©
  ¬∑ exact ‚ü®(alternatingConstHomologyDataOdd X _ h), isZero_zero C‚ü©

/-- The `n`-th homology of the alternating constant complex is `X` for `n = 0`. -/
noncomputable
def alternatingConstHomologyZero (X : C) : (alternatingConst.obj X).homology 0 ‚âÖ X :=
  (alternatingConstHomologyDataZero X _ rfl).left.homologyIso

end ChainComplex

variable [Preadditive C] [HasZeroObject C]

/-- The alternating face complex of the constant complex is the alternating constant complex. -/
def AlgebraicTopology.alternatingFaceMapComplexConst :
    Functor.const _ ‚ãô alternatingFaceMapComplex C ‚âÖ ChainComplex.alternatingConst :=
  NatIso.ofComponents (fun X ‚Ü¶ HomologicalComplex.Hom.isoOfComponents (fun _ ‚Ü¶ Iso.refl _) <| by
    rintro _ i rfl
    simp [SimplicialObject.Œ¥, ‚Üê Finset.sum_smul, Fin.sum_neg_one_pow, Nat.even_add_one,
      ‚Üê Nat.not_even_iff_odd]) (by intros; ext; simp)

namespace ChainComplex

/-- `alternatingConst.obj X` is homotopy equivalent to the chain
complex `(single‚ÇÄ C).obj X`. -/
noncomputable def alternatingConstHomotopyEquiv (X : C) :
    HomotopyEquiv (alternatingConst.obj X) ((single‚ÇÄ C).obj X) :=
  (HomotopyEquiv.ofIso (alternatingFaceMapComplexConst.app X).symm).trans
    ((SimplicialObject.Augmented.ExtraDegeneracy.const X).homotopyEquiv)

end ChainComplex
