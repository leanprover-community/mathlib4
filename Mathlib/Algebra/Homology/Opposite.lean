/-
Copyright (c) 2022 Amelia Livingston. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Amelia Livingston, Jo√´l Riou
-/
import Mathlib.CategoryTheory.Abelian.Opposite
import Mathlib.Algebra.Homology.Additive
import Mathlib.Algebra.Homology.ImageToKernel
import Mathlib.Algebra.Homology.ShortComplex.HomologicalComplex
import Mathlib.Algebra.Homology.QuasiIso

/-!
# Opposite categories of complexes
Given a preadditive category `V`, the opposite of its category of chain complexes is equivalent to
the category of cochain complexes of objects in `V·µí·µñ`. We define this equivalence, and another
analogous equivalence (for a general category of homological complexes with a general
complex shape).

We then show that when `V` is abelian, if `C` is a homological complex, then the homology of
`op(C)` is isomorphic to `op` of the homology of `C` (and the analogous result for `unop`).

## Implementation notes
It is convenient to define both `op` and `opSymm`; this is because given a complex shape `c`,
`c.symm.symm` is not defeq to `c`.

## Tags
opposite, chain complex, cochain complex, homology, cohomology, homological complex
-/


noncomputable section

open Opposite CategoryTheory CategoryTheory.Limits

section

variable {V : Type*} [Category V] [Abelian V]

theorem imageToKernel_op {X Y Z : V} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (w : f ‚â´ g = 0) :
    imageToKernel g.op f.op (by rw [‚Üê op_comp, w, op_zero]) =
      (imageSubobjectIso _ ‚â™‚â´ (imageOpOp _).symm).hom ‚â´
        (cokernel.desc f (factorThruImage g)
              (by rw [‚Üê cancel_mono (image.Œπ g), Category.assoc, image.fac, w, zero_comp])).op ‚â´
          (kernelSubobjectIso _ ‚â™‚â´ kernelOpOp _).inv := by
  ext
  simp only [Iso.trans_hom, Iso.symm_hom, Iso.trans_inv, kernelOpOp_inv, Category.assoc,
    imageToKernel_arrow, kernelSubobject_arrow', kernel.lift_Œπ, ‚Üê op_comp, cokernel.œÄ_desc,
    ‚Üê imageSubobject_arrow, ‚Üê imageUnopOp_inv_comp_op_factorThruImage g.op]
  rfl

theorem imageToKernel_unop {X Y Z : V·µí·µñ} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (w : f ‚â´ g = 0) :
    imageToKernel g.unop f.unop (by rw [‚Üê unop_comp, w, unop_zero]) =
      (imageSubobjectIso _ ‚â™‚â´ (imageUnopUnop _).symm).hom ‚â´
        (cokernel.desc f (factorThruImage g)
              (by rw [‚Üê cancel_mono (image.Œπ g), Category.assoc, image.fac, w, zero_comp])).unop ‚â´
          (kernelSubobjectIso _ ‚â™‚â´ kernelUnopUnop _).inv := by
  ext
  dsimp only [imageUnopUnop]
  simp only [Iso.trans_hom, Iso.symm_hom, Iso.trans_inv, kernelUnopUnop_inv, Category.assoc,
    imageToKernel_arrow, kernelSubobject_arrow', kernel.lift_Œπ, cokernel.œÄ_desc, Iso.unop_inv,
    ‚Üê unop_comp, factorThruImage_comp_imageUnopOp_inv, Quiver.Hom.unop_op, imageSubobject_arrow]

end

namespace HomologicalComplex

variable {Œπ V : Type*} [Category V] {c : ComplexShape Œπ}

section

variable [HasZeroMorphisms V]

/-- Sends a complex `X` with objects in `V` to the corresponding complex with objects in `V·µí·µñ`. -/
@[simps]
protected def op (X : HomologicalComplex V c) : HomologicalComplex V·µí·µñ c.symm where
  X i := op (X.X i)
  d i j := (X.d j i).op
  shape i j hij := by rw [X.shape j i hij, op_zero]
  d_comp_d' _ _ _ _ _ := by rw [‚Üê op_comp, X.d_comp_d, op_zero]

/-- Sends a complex `X` with objects in `V` to the corresponding complex with objects in `V·µí·µñ`. -/
@[simps]
protected def opSymm (X : HomologicalComplex V c.symm) : HomologicalComplex V·µí·µñ c where
  X i := op (X.X i)
  d i j := (X.d j i).op
  shape i j hij := by rw [X.shape j i hij, op_zero]
  d_comp_d' _ _ _ _ _ := by rw [‚Üê op_comp, X.d_comp_d, op_zero]

/-- Sends a complex `X` with objects in `V·µí·µñ` to the corresponding complex with objects in `V`. -/
@[simps]
protected def unop (X : HomologicalComplex V·µí·µñ c) : HomologicalComplex V c.symm where
  X i := unop (X.X i)
  d i j := (X.d j i).unop
  shape i j hij := by rw [X.shape j i hij, unop_zero]
  d_comp_d' _ _ _ _ _ := by rw [‚Üê unop_comp, X.d_comp_d, unop_zero]

/-- Sends a complex `X` with objects in `V·µí·µñ` to the corresponding complex with objects in `V`. -/
@[simps]
protected def unopSymm (X : HomologicalComplex V·µí·µñ c.symm) : HomologicalComplex V c where
  X i := unop (X.X i)
  d i j := (X.d j i).unop
  shape i j hij := by rw [X.shape j i hij, unop_zero]
  d_comp_d' _ _ _ _ _ := by rw [‚Üê unop_comp, X.d_comp_d, unop_zero]

variable (V c)

/-- Auxiliary definition for `opEquivalence`. -/
@[simps]
def opFunctor : (HomologicalComplex V c)·µí·µñ ‚•§ HomologicalComplex V·µí·µñ c.symm where
  obj X := (unop X).op
  map f :=
    { f := fun i => (f.unop.f i).op
      comm' := fun i j _ => by simp only [op_d, ‚Üê op_comp, f.unop.comm] }

/-- Auxiliary definition for `opEquivalence`. -/
@[simps]
def opInverse : HomologicalComplex V·µí·µñ c.symm ‚•§ (HomologicalComplex V c)·µí·µñ where
  obj X := op X.unopSymm
  map f := Quiver.Hom.op
    { f := fun i => (f.f i).unop
      comm' := fun i j _ => by simp only [unopSymm_d, ‚Üê unop_comp, f.comm] }

/-- Auxiliary definition for `opEquivalence`. -/
def opUnitIso : ùü≠ (HomologicalComplex V c)·µí·µñ ‚âÖ opFunctor V c ‚ãô opInverse V c :=
  NatIso.ofComponents
    (fun X =>
      (HomologicalComplex.Hom.isoOfComponents (fun _ => Iso.refl _) fun i j _ => by
            simp only [Iso.refl_hom, Category.id_comp, unopSymm_d, op_d, Quiver.Hom.unop_op,
              Category.comp_id] :
          (Opposite.unop X).op.unopSymm ‚âÖ unop X).op)
    (by
      intro X Y f
      refine Quiver.Hom.unop_inj ?_
      ext x
      simp)

/-- Auxiliary definition for `opEquivalence`. -/
def opCounitIso : opInverse V c ‚ãô opFunctor V c ‚âÖ ùü≠ (HomologicalComplex V·µí·µñ c.symm) :=
  NatIso.ofComponents
    fun X => HomologicalComplex.Hom.isoOfComponents fun _ => Iso.refl _

/-- Given a category of complexes with objects in `V`, there is a natural equivalence between its
opposite category and a category of complexes with objects in `V·µí·µñ`. -/
@[simps]
def opEquivalence : (HomologicalComplex V c)·µí·µñ ‚âå HomologicalComplex V·µí·µñ c.symm where
  functor := opFunctor V c
  inverse := opInverse V c
  unitIso := opUnitIso V c
  counitIso := opCounitIso V c
  functor_unitIso_comp X := by
    ext
    simp only [opUnitIso, opCounitIso, NatIso.ofComponents_hom_app, Iso.op_hom, comp_f,
      opFunctor_map_f, Quiver.Hom.unop_op, Hom.isoOfComponents_hom_f]
    exact Category.comp_id _

/-- Auxiliary definition for `unopEquivalence`. -/
@[simps]
def unopFunctor : (HomologicalComplex V·µí·µñ c)·µí·µñ ‚•§ HomologicalComplex V c.symm where
  obj X := (unop X).unop
  map f :=
    { f := fun i => (f.unop.f i).unop
      comm' := fun i j _ => by simp only [unop_d, ‚Üê unop_comp, f.unop.comm] }

/-- Auxiliary definition for `unopEquivalence`. -/
@[simps]
def unopInverse : HomologicalComplex V c.symm ‚•§ (HomologicalComplex V·µí·µñ c)·µí·µñ where
  obj X := op X.opSymm
  map f := Quiver.Hom.op
    { f := fun i => (f.f i).op
      comm' := fun i j _ => by simp only [opSymm_d, ‚Üê op_comp, f.comm] }

/-- Auxiliary definition for `unopEquivalence`. -/
def unopUnitIso : ùü≠ (HomologicalComplex V·µí·µñ c)·µí·µñ ‚âÖ unopFunctor V c ‚ãô unopInverse V c :=
  NatIso.ofComponents
    (fun X =>
      (HomologicalComplex.Hom.isoOfComponents (fun _ => Iso.refl _) fun i j _ => by
            simp only [Iso.refl_hom, Category.id_comp, unopSymm_d, op_d, Quiver.Hom.unop_op,
              Category.comp_id] :
          (Opposite.unop X).op.unopSymm ‚âÖ unop X).op)
    (by
      intro X Y f
      refine Quiver.Hom.unop_inj ?_
      ext x
      simp)

/-- Auxiliary definition for `unopEquivalence`. -/
def unopCounitIso : unopInverse V c ‚ãô unopFunctor V c ‚âÖ ùü≠ (HomologicalComplex V c.symm) :=
  NatIso.ofComponents
    fun X => HomologicalComplex.Hom.isoOfComponents fun _ => Iso.refl _

/-- Given a category of complexes with objects in `V·µí·µñ`, there is a natural equivalence between its
opposite category and a category of complexes with objects in `V`. -/
@[simps]
def unopEquivalence : (HomologicalComplex V·µí·µñ c)·µí·µñ ‚âå HomologicalComplex V c.symm where
  functor := unopFunctor V c
  inverse := unopInverse V c
  unitIso := unopUnitIso V c
  counitIso := unopCounitIso V c
  functor_unitIso_comp X := by
    ext
    simp only [comp_f]
    exact Category.comp_id _

instance (K : HomologicalComplex V c) (i : Œπ) [K.HasHomology i] :
    K.op.HasHomology i :=
  (inferInstance : (K.sc i).op.HasHomology)

instance (K : HomologicalComplex V·µí·µñ c) (i : Œπ) [K.HasHomology i] :
    K.unop.HasHomology i :=
  (inferInstance : (K.sc i).unop.HasHomology)

instance (K : HomologicalComplex V c) (i : Œπ) [K.HasHomology i] :
    ((opFunctor _ _).obj (op K)).HasHomology i := by
  dsimp
  infer_instance

instance (K : HomologicalComplex V·µí·µñ c) (i : Œπ) [K.HasHomology i] :
    ((unopFunctor _ _).obj (op K)).HasHomology i := by
  dsimp
  infer_instance

variable {V c}

@[simp]
lemma quasiIsoAt_opFunctor_map_iff
    {K L : HomologicalComplex V c} (œÜ : K ‚ü∂ L) (i : Œπ)
    [K.HasHomology i] [L.HasHomology i] :
    QuasiIsoAt ((opFunctor _ _).map œÜ.op) i ‚Üî QuasiIsoAt œÜ i := by
  simp only [quasiIsoAt_iff]
  exact ShortComplex.quasiIso_opMap_iff ((shortComplexFunctor V c i).map œÜ)

@[simp]
lemma quasiIsoAt_unopFunctor_map_iff
    {K L : HomologicalComplex V·µí·µñ c} (œÜ : K ‚ü∂ L) (i : Œπ)
    [K.HasHomology i] [L.HasHomology i] :
    QuasiIsoAt ((unopFunctor _ _).map œÜ.op) i ‚Üî QuasiIsoAt œÜ i := by
  rw [‚Üê quasiIsoAt_opFunctor_map_iff]
  rfl

instance {K L : HomologicalComplex V c} (œÜ : K ‚ü∂ L) (i : Œπ)
    [K.HasHomology i] [L.HasHomology i] [QuasiIsoAt œÜ i] :
    QuasiIsoAt ((opFunctor _ _).map œÜ.op) i := by
  rw [quasiIsoAt_opFunctor_map_iff]
  infer_instance

instance {K L : HomologicalComplex V·µí·µñ c} (œÜ : K ‚ü∂ L) (i : Œπ)
    [K.HasHomology i] [L.HasHomology i] [QuasiIsoAt œÜ i] :
    QuasiIsoAt ((unopFunctor _ _).map œÜ.op) i := by
  rw [quasiIsoAt_unopFunctor_map_iff]
  infer_instance

@[simp]
lemma quasiIso_opFunctor_map_iff
    {K L : HomologicalComplex V c} (œÜ : K ‚ü∂ L)
    [‚àÄ i, K.HasHomology i] [‚àÄ i, L.HasHomology i] :
    QuasiIso ((opFunctor _ _).map œÜ.op) ‚Üî QuasiIso œÜ := by
  simp only [quasiIso_iff, quasiIsoAt_opFunctor_map_iff]

@[simp]
lemma quasiIso_unopFunctor_map_iff
    {K L : HomologicalComplex V·µí·µñ c} (œÜ : K ‚ü∂ L)
    [‚àÄ i, K.HasHomology i] [‚àÄ i, L.HasHomology i] :
    QuasiIso ((unopFunctor _ _).map œÜ.op) ‚Üî QuasiIso œÜ := by
  simp only [quasiIso_iff, quasiIsoAt_unopFunctor_map_iff]

instance {K L : HomologicalComplex V c} (œÜ : K ‚ü∂ L)
    [‚àÄ i, K.HasHomology i] [‚àÄ i, L.HasHomology i] [QuasiIso œÜ] :
    QuasiIso ((opFunctor _ _).map œÜ.op) := by
  rw [quasiIso_opFunctor_map_iff]
  infer_instance

instance {K L : HomologicalComplex V·µí·µñ c} (œÜ : K ‚ü∂ L)
    [‚àÄ i, K.HasHomology i] [‚àÄ i, L.HasHomology i] [QuasiIso œÜ] :
    QuasiIso ((unopFunctor _ _).map œÜ.op) := by
  rw [quasiIso_unopFunctor_map_iff]
  infer_instance

lemma ExactAt.op {K : HomologicalComplex V c} {i : Œπ} (h : K.ExactAt i) :
    K.op.ExactAt i :=
  ShortComplex.Exact.op h

lemma ExactAt.unop {K : HomologicalComplex V·µí·µñ c} {i : Œπ} (h : K.ExactAt i) :
    K.unop.ExactAt i :=
  ShortComplex.Exact.unop h

@[simp]
lemma exactAt_op_iff (K : HomologicalComplex V c) {i : Œπ} :
    K.op.ExactAt i ‚Üî K.ExactAt i :=
  ‚ü®fun h ‚Ü¶ h.unop, fun h ‚Ü¶ h.op‚ü©

lemma Acyclic.op {K : HomologicalComplex V c} (h : K.Acyclic) :
    K.op.Acyclic :=
  fun i ‚Ü¶ (h i).op

lemma Acyclic.unop {K : HomologicalComplex V·µí·µñ c} (h : K.Acyclic) :
    K.unop.Acyclic :=
  fun i ‚Ü¶ (h i).unop

@[simp]
lemma acyclic_op_iff (K : HomologicalComplex V c) :
    K.op.Acyclic ‚Üî K.Acyclic :=
  ‚ü®fun h ‚Ü¶ h.unop, fun h ‚Ü¶ h.op‚ü©

/-- If `K` is a homological complex, then the homology of `K.op` identifies to
the opposite of the homology of `K`. -/
def homologyOp (K : HomologicalComplex V c) (i : Œπ) [K.HasHomology i] :
    K.op.homology i ‚âÖ op (K.homology i) :=
  (K.sc i).homologyOpIso

/-- If `K` is a homological complex in the opposite category,
then the homology of `K.unop` identifies to the opposite of the homology of `K`. -/
def homologyUnop (K : HomologicalComplex V·µí·µñ c) (i : Œπ) [K.HasHomology i] :
    K.unop.homology i ‚âÖ unop (K.homology i) :=
  (K.unop.homologyOp i).unop

section

variable (K : HomologicalComplex V c) (i : Œπ) [K.HasHomology i]

/-- The canonical isomorphism `K.op.cycles i ‚âÖ op (K.opcycles i)`. -/
def cyclesOpIso : K.op.cycles i ‚âÖ op (K.opcycles i) :=
  (K.sc i).cyclesOpIso

/-- The canonical isomorphism `K.op.opcycles i ‚âÖ op (K.cycles i)`. -/
def opcyclesOpIso : K.op.opcycles i ‚âÖ op (K.cycles i) :=
  (K.sc i).opcyclesOpIso

variable (j : Œπ)

@[reassoc (attr := simp)]
lemma opcyclesOpIso_hom_toCycles_op :
    (K.opcyclesOpIso i).hom ‚â´ (K.toCycles j i).op = K.op.fromOpcycles i j := by
  by_cases hij : c.Rel j i
  ¬∑ obtain rfl := c.prev_eq' hij
    exact (K.sc i).opcyclesOpIso_hom_toCycles_op
  ¬∑ rw [K.toCycles_eq_zero hij, K.op.fromOpcycles_eq_zero hij, op_zero, comp_zero]

@[reassoc (attr := simp)]
lemma fromOpcycles_op_cyclesOpIso_inv :
    (K.fromOpcycles i j).op ‚â´ (K.cyclesOpIso i).inv = K.op.toCycles j i := by
  by_cases hij : c.Rel i j
  ¬∑ obtain rfl := c.next_eq' hij
    exact (K.sc i).fromOpcycles_op_cyclesOpIso_inv
  ¬∑ rw [K.op.toCycles_eq_zero hij, K.fromOpcycles_eq_zero hij, op_zero, zero_comp]

end

section

variable {K L : HomologicalComplex V c} (œÜ : K ‚ü∂ L) (i : Œπ)
  [K.HasHomology i] [L.HasHomology i]

@[reassoc]
lemma homologyOp_hom_naturality :
    homologyMap ((opFunctor _ _).map œÜ.op) _ ‚â´ (K.homologyOp i).hom =
      (L.homologyOp i).hom ‚â´ (homologyMap œÜ i).op :=
  ShortComplex.homologyOpIso_hom_naturality ((shortComplexFunctor V c i).map œÜ)

@[reassoc]
lemma opcyclesOpIso_hom_naturality :
    opcyclesMap ((opFunctor _ _).map œÜ.op) _ ‚â´ (K.opcyclesOpIso i).hom =
      (L.opcyclesOpIso i).hom ‚â´ (cyclesMap œÜ i).op :=
  ShortComplex.opcyclesOpIso_hom_naturality ((shortComplexFunctor V c i).map œÜ)

@[reassoc]
lemma opcyclesOpIso_inv_naturality :
    (cyclesMap œÜ i).op ‚â´ (K.opcyclesOpIso i).inv =
      (L.opcyclesOpIso i).inv ‚â´ opcyclesMap ((opFunctor _ _).map œÜ.op) _ :=
  ShortComplex.opcyclesOpIso_inv_naturality ((shortComplexFunctor V c i).map œÜ)

@[reassoc]
lemma cyclesOpIso_hom_naturality :
    cyclesMap ((opFunctor _ _).map œÜ.op) _ ‚â´ (K.cyclesOpIso i).hom =
      (L.cyclesOpIso i).hom ‚â´ (opcyclesMap œÜ i).op :=
  ShortComplex.cyclesOpIso_hom_naturality ((shortComplexFunctor V c i).map œÜ)

@[reassoc]
lemma cyclesOpIso_inv_naturality :
    (opcyclesMap œÜ i).op ‚â´ (K.cyclesOpIso i).inv =
      (L.cyclesOpIso i).inv ‚â´ cyclesMap ((opFunctor _ _).map œÜ.op) _ :=
  ShortComplex.cyclesOpIso_inv_naturality ((shortComplexFunctor V c i).map œÜ)

end

section

variable (V c) [CategoryWithHomology V] (i : Œπ)

/-- The natural isomorphism `K.op.cycles i ‚âÖ op (K.opcycles i)`. -/
@[simps!]
def cyclesOpNatIso :
    opFunctor V c ‚ãô cyclesFunctor V·µí·µñ c.symm i ‚âÖ (opcyclesFunctor V c i).op :=
  NatIso.ofComponents (fun K ‚Ü¶ (unop K).cyclesOpIso i)
    (fun _ ‚Ü¶ cyclesOpIso_hom_naturality _ _)

/-- The natural isomorphism `K.op.opcycles i ‚âÖ op (K.cycles i)`. -/
def opcyclesOpNatIso :
    opFunctor V c ‚ãô opcyclesFunctor V·µí·µñ c.symm i ‚âÖ (cyclesFunctor V c i).op :=
  NatIso.ofComponents (fun K ‚Ü¶ (unop K).opcyclesOpIso i)
    (fun _ ‚Ü¶ opcyclesOpIso_hom_naturality _ _)

/-- The natural isomorphism `K.op.homology i ‚âÖ op (K.homology i)`. -/
def homologyOpNatIso :
    opFunctor V c ‚ãô homologyFunctor V·µí·µñ c.symm i ‚âÖ (homologyFunctor V c i).op :=
  NatIso.ofComponents (fun K ‚Ü¶ (unop K).homologyOp i)
    (fun _ ‚Ü¶ homologyOp_hom_naturality _ _)

end

end

section

variable [Preadditive V]

instance opFunctor_additive : (@opFunctor Œπ V _ c _).Additive where

instance unopFunctor_additive : (@unopFunctor Œπ V _ c _).Additive where

end

end HomologicalComplex
