/-
Copyright (c) 2021 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.Algebra.Homology.HomologicalComplex

/-!
# Homological complexes supported in a single degree

We define `single V j c : V ‚•§ HomologicalComplex V c`,
which constructs complexes in `V` of shape `c`, supported in degree `j`.

In `ChainComplex.toSingle‚ÇÄEquiv` we characterize chain maps to an
`‚Ñï`-indexed complex concentrated in degree 0; they are equivalent to
`{ f : C.X 0 ‚ü∂ X // C.d 1 0 ‚â´ f = 0 }`.
(This is useful translating between a projective resolution and
an augmented exact complex of projectives.)

-/

open CategoryTheory Category Limits ZeroObject

universe v u

variable (V : Type u) [Category.{v} V] [HasZeroMorphisms V] [HasZeroObject V]

namespace HomologicalComplex

variable {Œπ : Type*} [DecidableEq Œπ] (c : ComplexShape Œπ)

/-- The functor `V ‚•§ HomologicalComplex V c` creating a chain complex supported in a single degree.
-/
noncomputable def single (j : Œπ) : V ‚•§ HomologicalComplex V c where
  obj A :=
    { X := fun i => if i = j then A else 0
      d := fun _ _ => 0 }
  map f :=
    { f := fun i => if h : i = j then eqToHom (by dsimp; rw [if_pos h]) ‚â´ f ‚â´
              eqToHom (by dsimp; rw [if_pos h]) else 0 }
  map_id A := by
    ext
    dsimp
    split_ifs with h
    ¬∑ subst h
      simp
    ¬∑ #adaptation_note /-- nightly-2024-03-07
      previously was `rw [if_neg h]; simp`, but that fails with "motive not type correct"
      This is because dsimp does not simplify numerals;
      this note should be removable once https://github.com/leanprover/lean4/pull/8433 lands. -/
      convert (id_zero (C := V)).symm
      all_goals simp [if_neg h]
  map_comp f g := by
    ext
    dsimp
    split_ifs with h
    ¬∑ subst h
      simp
    ¬∑ simp

variable {V}

@[simp]
lemma single_obj_X_self (j : Œπ) (A : V) :
    ((single V c j).obj A).X j = A := if_pos rfl

lemma isZero_single_obj_X (j : Œπ) (A : V) (i : Œπ) (hi : i ‚â† j) :
    IsZero (((single V c j).obj A).X i) := by
  dsimp [single]
  rw [if_neg hi]
  exact Limits.isZero_zero V

/-- The object in degree `i` of `(single V c h).obj A` is just `A` when `i = j`. -/
noncomputable def singleObjXIsoOfEq (j : Œπ) (A : V) (i : Œπ) (hi : i = j) :
    ((single V c j).obj A).X i ‚âÖ A :=
  eqToIso (by subst hi; simp [single])

/-- The object in degree `j` of `(single V c h).obj A` is just `A`. -/
noncomputable def singleObjXSelf (j : Œπ) (A : V) : ((single V c j).obj A).X j ‚âÖ A :=
  singleObjXIsoOfEq c j A j rfl

@[simp]
lemma single_obj_d (j : Œπ) (A : V) (k l : Œπ) :
    ((single V c j).obj A).d k l = 0 := rfl

@[reassoc]
theorem single_map_f_self (j : Œπ) {A B : V} (f : A ‚ü∂ B) :
    ((single V c j).map f).f j = (singleObjXSelf c j A).hom ‚â´
      f ‚â´ (singleObjXSelf c j B).inv := by
  dsimp [single]
  rw [dif_pos rfl]
  rfl

variable (V)

/-- The natural isomorphism `single V c j ‚ãô eval V c j ‚âÖ ùü≠ V`. -/
@[simps!]
noncomputable def singleCompEvalIsoSelf (j : Œπ) : single V c j ‚ãô eval V c j ‚âÖ ùü≠ V :=
  NatIso.ofComponents (singleObjXSelf c j) (fun {A B} f => by simp [single_map_f_self])

lemma isZero_single_comp_eval (j i : Œπ) (hi : i ‚â† j) : IsZero (single V c j ‚ãô eval V c i) :=
  Functor.isZero _ (fun _ ‚Ü¶ isZero_single_obj_X c _ _ _ hi)

variable {V c}

@[ext]
lemma from_single_hom_ext {K : HomologicalComplex V c} {j : Œπ} {A : V}
    {f g : (single V c j).obj A ‚ü∂ K} (hfg : f.f j = g.f j) : f = g := by
  ext i
  by_cases h : i = j
  ¬∑ subst h
    exact hfg
  ¬∑ apply (isZero_single_obj_X c j A i h).eq_of_src

@[ext]
lemma to_single_hom_ext {K : HomologicalComplex V c} {j : Œπ} {A : V}
    {f g : K ‚ü∂ (single V c j).obj A} (hfg : f.f j = g.f j) : f = g := by
  ext i
  by_cases h : i = j
  ¬∑ subst h
    exact hfg
  ¬∑ apply (isZero_single_obj_X c j A i h).eq_of_tgt

instance (j : Œπ) : (single V c j).Faithful where
  map_injective {A B f g} w := by
    rw [‚Üê cancel_mono (singleObjXSelf c j B).inv,
      ‚Üê cancel_epi (singleObjXSelf c j A).hom, ‚Üê single_map_f_self,
      ‚Üê single_map_f_self, w]

instance (j : Œπ) : (single V c j).Full where
  map_surjective {A B} f :=
    ‚ü®(singleObjXSelf c j A).inv ‚â´ f.f j ‚â´ (singleObjXSelf c j B).hom, by
      ext
      simp [single_map_f_self]‚ü©

/-- Constructor for morphisms to a single homological complex. -/
noncomputable def mkHomToSingle {K : HomologicalComplex V c} {j : Œπ} {A : V} (œÜ : K.X j ‚ü∂ A)
    (hœÜ : ‚àÄ (i : Œπ), c.Rel i j ‚Üí K.d i j ‚â´ œÜ = 0) :
    K ‚ü∂ (single V c j).obj A where
  f i :=
    if hi : i = j
      then (K.XIsoOfEq hi).hom ‚â´ œÜ ‚â´ (singleObjXIsoOfEq c j A i hi).inv
      else 0
  comm' i k hik := by
    dsimp
    rw [comp_zero]
    split_ifs with hk
    ¬∑ subst hk
      simp only [XIsoOfEq_rfl, Iso.refl_hom, id_comp, reassoc_of% hœÜ i hik, zero_comp]
    ¬∑ apply (isZero_single_obj_X c j A k hk).eq_of_tgt

@[simp]
lemma mkHomToSingle_f {K : HomologicalComplex V c} {j : Œπ} {A : V} (œÜ : K.X j ‚ü∂ A)
    (hœÜ : ‚àÄ (i : Œπ), c.Rel i j ‚Üí K.d i j ‚â´ œÜ = 0) :
    (mkHomToSingle œÜ hœÜ).f j = œÜ ‚â´ (singleObjXSelf c j A).inv := by
  dsimp [mkHomToSingle]
  rw [dif_pos rfl, id_comp]
  rfl

/-- Constructor for morphisms from a single homological complex. -/
noncomputable def mkHomFromSingle {K : HomologicalComplex V c} {j : Œπ} {A : V} (œÜ : A ‚ü∂ K.X j)
    (hœÜ : ‚àÄ (k : Œπ), c.Rel j k ‚Üí œÜ ‚â´ K.d j k = 0) :
    (single V c j).obj A ‚ü∂ K where
  f i :=
    if hi : i = j
      then (singleObjXIsoOfEq c j A i hi).hom ‚â´ œÜ ‚â´ (K.XIsoOfEq hi).inv
      else 0
  comm' i k hik := by
    dsimp
    rw [zero_comp]
    split_ifs with hi
    ¬∑ subst hi
      simp only [XIsoOfEq_rfl, Iso.refl_inv, comp_id, assoc, hœÜ k hik, comp_zero]
    ¬∑ apply (isZero_single_obj_X c j A i hi).eq_of_src

@[simp]
lemma mkHomFromSingle_f {K : HomologicalComplex V c} {j : Œπ} {A : V} (œÜ : A ‚ü∂ K.X j)
    (hœÜ : ‚àÄ (k : Œπ), c.Rel j k ‚Üí œÜ ‚â´ K.d j k = 0) :
    (mkHomFromSingle œÜ hœÜ).f j = (singleObjXSelf c j A).hom ‚â´ œÜ := by
  dsimp [mkHomFromSingle]
  rw [dif_pos rfl, comp_id]
  rfl

instance (j : Œπ) : (single V c j).PreservesZeroMorphisms where

end HomologicalComplex

namespace ChainComplex

/-- The functor `V ‚•§ ChainComplex V ‚Ñï` creating a chain complex supported in degree zero. -/
noncomputable abbrev single‚ÇÄ : V ‚•§ ChainComplex V ‚Ñï :=
  HomologicalComplex.single V (ComplexShape.down ‚Ñï) 0

variable {V}

@[simp]
lemma single‚ÇÄ_obj_zero (A : V) :
    ((single‚ÇÄ V).obj A).X 0 = A := rfl

@[simp]
lemma single‚ÇÄ_map_f_zero {A B : V} (f : A ‚ü∂ B) :
    ((single‚ÇÄ V).map f).f 0 = f := by
  rw [HomologicalComplex.single_map_f_self]
  dsimp [HomologicalComplex.singleObjXSelf, HomologicalComplex.singleObjXIsoOfEq]
  rw [comp_id, id_comp]


@[simp]
lemma single‚ÇÄObjXSelf (X : V) :
    HomologicalComplex.singleObjXSelf (ComplexShape.down ‚Ñï) 0 X = Iso.refl _ := rfl

/-- Morphisms from an `‚Ñï`-indexed chain complex `C`
to a single object chain complex with `X` concentrated in degree 0
are the same as morphisms `f : C.X 0 ‚ü∂ X` such that `C.d 1 0 ‚â´ f = 0`.
-/
@[simps apply_coe]
noncomputable def toSingle‚ÇÄEquiv (C : ChainComplex V ‚Ñï) (X : V) :
    (C ‚ü∂ (single‚ÇÄ V).obj X) ‚âÉ { f : C.X 0 ‚ü∂ X // C.d 1 0 ‚â´ f = 0 } where
  toFun œÜ := ‚ü®œÜ.f 0, by rw [‚Üê œÜ.comm 1 0, HomologicalComplex.single_obj_d, comp_zero]‚ü©
  invFun f := HomologicalComplex.mkHomToSingle f.1 (fun i hi => by
    obtain rfl : i = 1 := by simpa using hi.symm
    exact f.2)
  left_inv œÜ := by aesop_cat
  right_inv f := by simp

@[simp]
lemma toSingle‚ÇÄEquiv_symm_apply_f_zero {C : ChainComplex V ‚Ñï} {X : V}
    (f : C.X 0 ‚ü∂ X) (hf : C.d 1 0 ‚â´ f = 0) :
    ((toSingle‚ÇÄEquiv C X).symm ‚ü®f, hf‚ü©).f 0 = f := by
  simp [toSingle‚ÇÄEquiv]

/-- Morphisms from a single object chain complex with `X` concentrated in degree 0
to an `‚Ñï`-indexed chain complex `C` are the same as morphisms `f : X ‚Üí C.X 0`.
-/
@[simps apply]
noncomputable def fromSingle‚ÇÄEquiv (C : ChainComplex V ‚Ñï) (X : V) :
    ((single‚ÇÄ V).obj X ‚ü∂ C) ‚âÉ (X ‚ü∂ C.X 0) where
  toFun f := f.f 0
  invFun f := HomologicalComplex.mkHomFromSingle f (fun i hi => by simp at hi)
  left_inv := by aesop_cat
  right_inv := by aesop_cat

@[simp]
lemma fromSingle‚ÇÄEquiv_symm_apply_f_zero
    {C : ChainComplex V ‚Ñï} {X : V} (f : X ‚ü∂ C.X 0) :
    ((fromSingle‚ÇÄEquiv C X).symm f).f 0 = f := by
  simp [fromSingle‚ÇÄEquiv]

@[simp]
lemma fromSingle‚ÇÄEquiv_symm_apply_f_succ
    {C : ChainComplex V ‚Ñï} {X : V} (f : X ‚ü∂ C.X 0) (n : ‚Ñï) :
    ((fromSingle‚ÇÄEquiv C X).symm f).f (n + 1) = 0 := rfl

end ChainComplex

namespace CochainComplex

/-- The functor `V ‚•§ CochainComplex V ‚Ñï` creating a cochain complex supported in degree zero. -/
noncomputable abbrev single‚ÇÄ : V ‚•§ CochainComplex V ‚Ñï :=
  HomologicalComplex.single V (ComplexShape.up ‚Ñï) 0

variable {V}

@[simp]
lemma single‚ÇÄ_obj_zero (A : V) :
    ((single‚ÇÄ V).obj A).X 0 = A := rfl

@[simp]
lemma single‚ÇÄ_map_f_zero {A B : V} (f : A ‚ü∂ B) :
    ((single‚ÇÄ V).map f).f 0 = f := by
  rw [HomologicalComplex.single_map_f_self]
  dsimp [HomologicalComplex.singleObjXSelf, HomologicalComplex.singleObjXIsoOfEq]
  rw [comp_id, id_comp]

@[simp]
lemma single‚ÇÄObjXSelf (X : V) :
    HomologicalComplex.singleObjXSelf (ComplexShape.up ‚Ñï) 0 X = Iso.refl _ := rfl

/-- Morphisms from a single object cochain complex with `X` concentrated in degree 0
to an `‚Ñï`-indexed cochain complex `C`
are the same as morphisms `f : X ‚ü∂ C.X 0` such that `f ‚â´ C.d 0 1 = 0`. -/
@[simps apply_coe]
noncomputable def fromSingle‚ÇÄEquiv (C : CochainComplex V ‚Ñï) (X : V) :
    ((single‚ÇÄ V).obj X ‚ü∂ C) ‚âÉ { f : X ‚ü∂ C.X 0 // f ‚â´ C.d 0 1 = 0 } where
  toFun œÜ := ‚ü®œÜ.f 0, by rw [œÜ.comm 0 1, HomologicalComplex.single_obj_d, zero_comp]‚ü©
  invFun f := HomologicalComplex.mkHomFromSingle f.1 (fun i hi => by
    obtain rfl : i = 1 := by simpa using hi.symm
    exact f.2)
  left_inv œÜ := by aesop_cat
  right_inv := by aesop_cat

@[simp]
lemma fromSingle‚ÇÄEquiv_symm_apply_f_zero {C : CochainComplex V ‚Ñï} {X : V}
    (f : X ‚ü∂ C.X 0) (hf : f ‚â´ C.d 0 1 = 0) :
    ((fromSingle‚ÇÄEquiv C X).symm ‚ü®f, hf‚ü©).f 0 = f := by
  simp [fromSingle‚ÇÄEquiv]

/-- Morphisms to a single object cochain complex with `X` concentrated in degree 0
to an `‚Ñï`-indexed cochain complex `C` are the same as morphisms `f : C.X 0 ‚ü∂ X`.
-/
@[simps apply]
noncomputable def toSingle‚ÇÄEquiv (C : CochainComplex V ‚Ñï) (X : V) :
    (C ‚ü∂ (single‚ÇÄ V).obj X) ‚âÉ (C.X 0 ‚ü∂ X) where
  toFun f := f.f 0
  invFun f := HomologicalComplex.mkHomToSingle f (fun i hi => by simp at hi)
  left_inv := by aesop_cat
  right_inv := by aesop_cat

@[simp]
lemma toSingle‚ÇÄEquiv_symm_apply_f_zero
    {C : CochainComplex V ‚Ñï} {X : V} (f : C.X 0 ‚ü∂ X) :
    ((toSingle‚ÇÄEquiv C X).symm f).f 0 = f := by
  simp [toSingle‚ÇÄEquiv]

@[simp]
lemma toSingle‚ÇÄEquiv_symm_apply_f_succ
    {C : CochainComplex V ‚Ñï} {X : V} (f : C.X 0 ‚ü∂ X) (n : ‚Ñï) :
    ((toSingle‚ÇÄEquiv C X).symm f).f (n + 1) = 0 := by
  rfl

end CochainComplex
