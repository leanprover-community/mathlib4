/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Algebra.Homology.Homology

#align_import algebra.homology.single from "leanprover-community/mathlib"@"324a7502510e835cdbd3de1519b6c66b51fb2467"

/-!
# Chain complexes supported in a single degree

We define `single V j c : V ‚•§ HomologicalComplex V c`,
which constructs complexes in `V` of shape `c`, supported in degree `j`.

Similarly `single‚ÇÄ V : V ‚•§ ChainComplex V ‚Ñï` is the special case for
`‚Ñï`-indexed chain complexes, with the object supported in degree `0`,
but with better definitional properties.

In `toSingle‚ÇÄEquiv` we characterize chain maps to an `‚Ñï`-indexed complex concentrated in degree 0;
they are equivalent to `{ f : C.X 0 ‚ü∂ X // C.d 1 0 ‚â´ f = 0 }`.
(This is useful translating between a projective resolution and
an augmented exact complex of projectives.)
-/


noncomputable section

open CategoryTheory

open CategoryTheory.Limits

open ZeroObject

universe v u

variable (V : Type u) [Category.{v} V] [HasZeroMorphisms V] [HasZeroObject V]

namespace HomologicalComplex

variable {Œπ : Type*} [DecidableEq Œπ] (c : ComplexShape Œπ)

/-- The functor `V ‚•§ HomologicalComplex V c` creating a chain complex supported in a single degree.

See also `ChainComplex.single‚ÇÄ : V ‚•§ ChainComplex V ‚Ñï`,
which has better definitional properties,
if you are working with `‚Ñï`-indexed complexes.
-/
@[simps]
def single (j : Œπ) : V ‚•§ HomologicalComplex V c where
  obj A :=
    { X := fun i => if i = j then A else 0
      d := fun i j => 0 }
  map f :=
    { f := fun i => if h : i = j then eqToHom (by dsimp; rw [if_pos h]) ‚â´ f ‚â´
              eqToHom (by dsimp; rw [if_pos h]) else 0 }
  map_id A := by
    ext
    dsimp
    split_ifs with h
    ¬∑ subst h
      simp
    ¬∑ rw [if_neg h]
      simp
  map_comp f g := by
    ext
    dsimp
    split_ifs with h
    ¬∑ subst h
      simp
    ¬∑ simp
#align homological_complex.single HomologicalComplex.single

/-- The object in degree `j` of `(single V c h).obj A` is just `A`.
-/
@[simps!]
def singleObjXSelf (j : Œπ) (A : V) : ((single V c j).obj A).X j ‚âÖ A :=
  eqToIso (by simp)
set_option linter.uppercaseLean3 false in
#align homological_complex.single_obj_X_self HomologicalComplex.singleObjXSelf

@[simp 1100]
theorem single_map_f_self (j : Œπ) {A B : V} (f : A ‚ü∂ B) :
    ((single V c j).map f).f j = (singleObjXSelf V c j A).hom ‚â´
      f ‚â´ (singleObjXSelf V c j B).inv := by simp
#align homological_complex.single_map_f_self HomologicalComplex.single_map_f_self

instance (j : Œπ) : Faithful (single V c j) where
  map_injective w := by
    have := congr_hom w j
    dsimp at this
    simp only [dif_pos] at this
    rw [‚Üê IsIso.inv_comp_eq, inv_eqToHom, eqToHom_trans_assoc, eqToHom_refl,
      Category.id_comp, ‚Üê IsIso.comp_inv_eq, Category.assoc, inv_eqToHom, eqToHom_trans,
      eqToHom_refl, Category.comp_id] at this
    exact this

instance (j : Œπ) : Full (single V c j) where
  preimage f := eqToHom (by simp) ‚â´ f.f j ‚â´ eqToHom (by simp)
  witness f := by
    ext i
    dsimp
    split_ifs with h
    ¬∑ subst h
      simp
    ¬∑ symm
      apply zero_of_target_iso_zero
      dsimp
      rw [if_neg h]

end HomologicalComplex

open HomologicalComplex

namespace ChainComplex

/-- `ChainComplex.single‚ÇÄ V` is the embedding of `V` into `ChainComplex V ‚Ñï`
as chain complexes supported in degree 0.

This is naturally isomorphic to `single V _ 0`, but has better definitional properties.
-/
def single‚ÇÄ : V ‚•§ ChainComplex V ‚Ñï where
  obj X :=
    { X := fun n =>
        match n with
        | 0 => X
        | _ + 1 => 0
      d := fun i j => 0 }
  map f :=
    { f := fun n =>
        match n with
        | 0 => f
        | n + 1 => 0 }
#align chain_complex.single‚ÇÄ ChainComplex.single‚ÇÄ

@[simp]
theorem single‚ÇÄ_obj_X_0 (X : V) : ((single‚ÇÄ V).obj X).X 0 = X :=
  rfl
set_option linter.uppercaseLean3 false in
#align chain_complex.single‚ÇÄ_obj_X_0 ChainComplex.single‚ÇÄ_obj_X_0

@[simp]
theorem single‚ÇÄ_obj_X_succ (X : V) (n : ‚Ñï) : ((single‚ÇÄ V).obj X).X (n + 1) = 0 :=
  rfl
set_option linter.uppercaseLean3 false in
#align chain_complex.single‚ÇÄ_obj_X_succ ChainComplex.single‚ÇÄ_obj_X_succ

@[simp]
theorem single‚ÇÄ_obj_X_d (X : V) (i j : ‚Ñï) : ((single‚ÇÄ V).obj X).d i j = 0 :=
  rfl
set_option linter.uppercaseLean3 false in
#align chain_complex.single‚ÇÄ_obj_X_d ChainComplex.single‚ÇÄ_obj_X_d

@[simp]
theorem single‚ÇÄ_obj_X_dTo (X : V) (j : ‚Ñï) : ((single‚ÇÄ V).obj X).dTo j = 0 := by
  rw [dTo_eq ((single‚ÇÄ V).obj X) rfl]
  simp
set_option linter.uppercaseLean3 false in
#align chain_complex.single‚ÇÄ_obj_X_d_to ChainComplex.single‚ÇÄ_obj_X_dTo

@[simp]
theorem single‚ÇÄ_obj_x_dFrom (X : V) (i : ‚Ñï) : ((single‚ÇÄ V).obj X).dFrom i = 0 := by
  cases i
  ¬∑ rw [dFrom_eq_zero]
    simp
  ¬∑ erw [dFrom_eq ((single‚ÇÄ V).obj X) rfl]
    simp
set_option linter.uppercaseLean3 false in
#align chain_complex.single‚ÇÄ_obj_X_d_from ChainComplex.single‚ÇÄ_obj_x_dFrom

@[simp]
theorem single‚ÇÄ_map_f_0 {X Y : V} (f : X ‚ü∂ Y) : ((single‚ÇÄ V).map f).f 0 = f :=
  rfl
#align chain_complex.single‚ÇÄ_map_f_0 ChainComplex.single‚ÇÄ_map_f_0

@[simp]
theorem single‚ÇÄ_map_f_succ {X Y : V} (f : X ‚ü∂ Y) (n : ‚Ñï) : ((single‚ÇÄ V).map f).f (n + 1) = 0 :=
  rfl
#align chain_complex.single‚ÇÄ_map_f_succ ChainComplex.single‚ÇÄ_map_f_succ

section

variable [HasEqualizers V] [HasCokernels V] [HasImages V] [HasImageMaps V]

/-- Sending objects to chain complexes supported at `0` then taking `0`-th homology
is the same as doing nothing.
-/
noncomputable def homology'Functor0Single‚ÇÄ : single‚ÇÄ V ‚ãô homology'Functor V _ 0 ‚âÖ ùü≠ V :=
  NatIso.ofComponents (fun X => homology'.congr _ _ (by simp) (by simp) ‚â™‚â´ homology'ZeroZero)
    fun f => by
      -- Porting note: why can't `aesop_cat` do this?
      dsimp
      ext
      simp
#align chain_complex.homology_functor_0_single‚ÇÄ ChainComplex.homology'Functor0Single‚ÇÄ

/-- Sending objects to chain complexes supported at `0` then taking `(n+1)`-st homology
is the same as the zero functor.
-/
noncomputable def homology'FunctorSuccSingle‚ÇÄ (n : ‚Ñï) :
    single‚ÇÄ V ‚ãô homology'Functor V _ (n + 1) ‚âÖ 0 :=
  NatIso.ofComponents
    (fun X =>
      homology'.congr _ _ (by simp) (by simp) ‚â™‚â´
        homology'ZeroZero ‚â™‚â´ (Functor.zero_obj _).isoZero.symm)
    fun f => (Functor.zero_obj _).eq_of_tgt _ _
#align chain_complex.homology_functor_succ_single‚ÇÄ ChainComplex.homology'FunctorSuccSingle‚ÇÄ

end

variable {V}

/-- Morphisms from an `‚Ñï`-indexed chain complex `C`
to a single object chain complex with `X` concentrated in degree 0
are the same as morphisms `f : C.X 0 ‚ü∂ X` such that `C.d 1 0 ‚â´ f = 0`.
-/
@[simps]
def toSingle‚ÇÄEquiv (C : ChainComplex V ‚Ñï) (X : V) :
    (C ‚ü∂ (single‚ÇÄ V).obj X) ‚âÉ { f : C.X 0 ‚ü∂ X // C.d 1 0 ‚â´ f = 0 } where
  toFun f :=
    ‚ü®f.f 0, by
      rw [‚Üê f.comm 1 0]
      simp‚ü©
  invFun f :=
    { f := fun i =>
        match i with
        | 0 => f.1
        | n + 1 => 0
      comm' := fun i j h => by
        rcases i with (_|_|i) <;> cases j <;> simp only [single‚ÇÄ_obj_X_d, comp_zero]
        ¬∑ rw [C.shape, zero_comp]
          simp
        ¬∑ exact f.2.symm
        ¬∑ rw [C.shape, zero_comp]
          exact i.succ_succ_ne_one.symm }
  left_inv f := by
    ext i
    rcases i with ‚ü®‚ü©
    ¬∑ rfl
    ¬∑ dsimp
      ext
  right_inv := by aesop_cat
#align chain_complex.to_single‚ÇÄ_equiv ChainComplex.toSingle‚ÇÄEquiv

@[ext]
theorem to_single‚ÇÄ_ext {C : ChainComplex V ‚Ñï} {X : V} (f g : C ‚ü∂ (single‚ÇÄ V).obj X)
    (h : f.f 0 = g.f 0) : f = g :=
  (toSingle‚ÇÄEquiv C X).injective
    (by
      ext
      exact h)
#align chain_complex.to_single‚ÇÄ_ext ChainComplex.to_single‚ÇÄ_ext

/-- Morphisms from a single object chain complex with `X` concentrated in degree 0
to an `‚Ñï`-indexed chain complex `C` are the same as morphisms `f : X ‚Üí C.X`.
-/
@[simps]
def fromSingle‚ÇÄEquiv (C : ChainComplex V ‚Ñï) (X : V) : ((single‚ÇÄ V).obj X ‚ü∂ C) ‚âÉ (X ‚ü∂ C.X 0) where
  toFun f := f.f 0
  invFun f :=
    { f := fun i =>
        match i with
        | 0 => f
        | n + 1 => 0
      comm' := fun i j h => by
        cases i <;> cases j <;>
          simp only [shape, ComplexShape.down_Rel, Nat.one_ne_zero, not_false_iff,
            zero_comp, single‚ÇÄ_obj_X_d, Nat.zero_eq, add_eq_zero, comp_zero] }
  left_inv f := by
    ext i
    cases i
    ¬∑ rfl
    ¬∑ dsimp
      ext
  right_inv g := rfl
#align chain_complex.from_single‚ÇÄ_equiv ChainComplex.fromSingle‚ÇÄEquiv

variable (V)

/-- `single‚ÇÄ` is the same as `single V _ 0`. -/
def single‚ÇÄIsoSingle : single‚ÇÄ V ‚âÖ single V _ 0 :=
  NatIso.ofComponents
    (fun X =>
      { hom := { f := fun i => by cases i <;> exact ùüô _ }
        inv := { f := fun i => by cases i <;> exact ùüô _ }
        hom_inv_id := to_single‚ÇÄ_ext _ _ (by simp)
        inv_hom_id := by
          ext (_|_)
          ¬∑ dsimp
            simp
          ¬∑ dsimp
            rw [Category.comp_id] })
    fun f => by ext (_|_) <;> aesop_cat
#align chain_complex.single‚ÇÄ_iso_single ChainComplex.single‚ÇÄIsoSingle

instance : Faithful (single‚ÇÄ V) :=
  Faithful.of_iso (single‚ÇÄIsoSingle V).symm

instance : Full (single‚ÇÄ V) :=
  Full.ofIso (single‚ÇÄIsoSingle V).symm

end ChainComplex

namespace CochainComplex

/-- `CochainComplex.single‚ÇÄ V` is the embedding of `V` into `CochainComplex V ‚Ñï`
as cochain complexes supported in degree 0.

This is naturally isomorphic to `single V _ 0`, but has better definitional properties.
-/
def single‚ÇÄ : V ‚•§ CochainComplex V ‚Ñï where
  obj X :=
    { X := fun n =>
        match n with
        | 0 => X
        | _ + 1 => 0
      d := fun i j => 0 }
  map f :=
    { f := fun n =>
        match n with
        | 0 => f
        | n + 1 => 0 }
#align cochain_complex.single‚ÇÄ CochainComplex.single‚ÇÄ

@[simp]
theorem single‚ÇÄ_obj_X_0 (X : V) : ((single‚ÇÄ V).obj X).X 0 = X :=
  rfl
set_option linter.uppercaseLean3 false in
#align cochain_complex.single‚ÇÄ_obj_X_0 CochainComplex.single‚ÇÄ_obj_X_0

@[simp]
theorem single‚ÇÄ_obj_X_succ (X : V) (n : ‚Ñï) : ((single‚ÇÄ V).obj X).X (n + 1) = 0 :=
  rfl
set_option linter.uppercaseLean3 false in
#align cochain_complex.single‚ÇÄ_obj_X_succ CochainComplex.single‚ÇÄ_obj_X_succ

@[simp]
theorem single‚ÇÄ_obj_X_d (X : V) (i j : ‚Ñï) : ((single‚ÇÄ V).obj X).d i j = 0 :=
  rfl
set_option linter.uppercaseLean3 false in
#align cochain_complex.single‚ÇÄ_obj_X_d CochainComplex.single‚ÇÄ_obj_X_d

@[simp]
theorem single‚ÇÄ_obj_x_dFrom (X : V) (j : ‚Ñï) : ((single‚ÇÄ V).obj X).dFrom j = 0 := by
  rw [dFrom_eq ((single‚ÇÄ V).obj X) rfl]
  simp
set_option linter.uppercaseLean3 false in
#align cochain_complex.single‚ÇÄ_obj_X_d_from CochainComplex.single‚ÇÄ_obj_x_dFrom

@[simp]
theorem single‚ÇÄ_obj_x_dTo (X : V) (i : ‚Ñï) : ((single‚ÇÄ V).obj X).dTo i = 0 := by
  cases i
  ¬∑ rw [dTo_eq_zero]
    simp
  ¬∑ erw [dTo_eq ((single‚ÇÄ V).obj X) rfl]
    simp
set_option linter.uppercaseLean3 false in
#align cochain_complex.single‚ÇÄ_obj_X_d_to CochainComplex.single‚ÇÄ_obj_x_dTo

@[simp]
theorem single‚ÇÄ_map_f_0 {X Y : V} (f : X ‚ü∂ Y) : ((single‚ÇÄ V).map f).f 0 = f :=
  rfl
#align cochain_complex.single‚ÇÄ_map_f_0 CochainComplex.single‚ÇÄ_map_f_0

@[simp]
theorem single‚ÇÄ_map_f_succ {X Y : V} (f : X ‚ü∂ Y) (n : ‚Ñï) : ((single‚ÇÄ V).map f).f (n + 1) = 0 :=
  rfl
#align cochain_complex.single‚ÇÄ_map_f_succ CochainComplex.single‚ÇÄ_map_f_succ

section

variable [HasEqualizers V] [HasCokernels V] [HasImages V] [HasImageMaps V]

/-- Sending objects to cochain complexes supported at `0` then taking `0`-th homology
is the same as doing nothing.
-/
noncomputable def homologyFunctor0Single‚ÇÄ : single‚ÇÄ V ‚ãô homology'Functor V _ 0 ‚âÖ ùü≠ V :=
  NatIso.ofComponents (fun X => homology'.congr _ _ (by simp) (by simp) ‚â™‚â´ homology'ZeroZero)
    fun f => by
      -- Porting note: why can't `aesop_cat` do this?
      dsimp
      ext
      simp
#align cochain_complex.homology_functor_0_single‚ÇÄ CochainComplex.homologyFunctor0Single‚ÇÄ

/-- Sending objects to cochain complexes supported at `0` then taking `(n+1)`-st homology
is the same as the zero functor.
-/
noncomputable def homology'FunctorSuccSingle‚ÇÄ (n : ‚Ñï) :
    single‚ÇÄ V ‚ãô homology'Functor V _ (n + 1) ‚âÖ 0 :=
  NatIso.ofComponents
    (fun X =>
      homology'.congr _ _ (by simp) (by simp) ‚â™‚â´
        homology'ZeroZero ‚â™‚â´ (Functor.zero_obj _).isoZero.symm)
    fun f => (Functor.zero_obj _).eq_of_tgt _ _
#align cochain_complex.homology_functor_succ_single‚ÇÄ CochainComplex.homology'FunctorSuccSingle‚ÇÄ

end

variable {V}

/-- Morphisms from a single object cochain complex with `X` concentrated in degree 0
to an `‚Ñï`-indexed cochain complex `C`
are the same as morphisms `f : X ‚ü∂ C.X 0` such that `f ‚â´ C.d 0 1 = 0`.
-/
def fromSingle‚ÇÄEquiv (C : CochainComplex V ‚Ñï) (X : V) :
    ((single‚ÇÄ V).obj X ‚ü∂ C) ‚âÉ { f : X ‚ü∂ C.X 0 // f ‚â´ C.d 0 1 = 0 } where
  toFun f :=
    ‚ü®f.f 0, by
      rw [f.comm 0 1]
      simp‚ü©
  invFun f :=
    { f := fun i =>
        match i with
        | 0 => f.1
        | n + 1 => 0
      comm' := fun i j h => by
        rcases f with ‚ü®f, hf‚ü©
        rcases j with (_|_|j) <;> cases i <;> simp only [single‚ÇÄ_obj_X_d, zero_comp]
        ¬∑ rw [C.shape, comp_zero]
          simp
        ¬∑ exact hf
        ¬∑ rw [C.shape, comp_zero]
          simp only [Nat.zero_eq, ComplexShape.up_Rel, zero_add]
          exact j.succ_succ_ne_one.symm }
  left_inv f := by
    ext i
    rcases i with ‚ü®‚ü©
    ¬∑ rfl
    ¬∑ dsimp
      ext
  right_inv := by aesop_cat
#align cochain_complex.from_single‚ÇÄ_equiv CochainComplex.fromSingle‚ÇÄEquiv

-- porting note: added to ease the following definition
@[ext]
theorem from_single‚ÇÄ_ext {C : CochainComplex V ‚Ñï} {X : V} (f g : (single‚ÇÄ V).obj X ‚ü∂ C)
    (h : f.f 0 = g.f 0) : f = g :=
  (fromSingle‚ÇÄEquiv C X).injective
    (by
      ext
      exact h)

variable (V)

/-- `single‚ÇÄ` is the same as `single V _ 0`. -/
def single‚ÇÄIsoSingle : single‚ÇÄ V ‚âÖ single V _ 0 :=
  NatIso.ofComponents fun X =>
    { hom := { f := fun i => by cases i <;> exact ùüô _ }
      inv := { f := fun i => by cases i <;> exact ùüô _ }
      hom_inv_id := from_single‚ÇÄ_ext _ _ (by simp)
      inv_hom_id := by
        ext (_|_)
        ¬∑ dsimp
          simp
        ¬∑ dsimp
          rw [Category.id_comp]
          rfl }
#align cochain_complex.single‚ÇÄ_iso_single CochainComplex.single‚ÇÄIsoSingle

instance : Faithful (single‚ÇÄ V) :=
  Faithful.of_iso (single‚ÇÄIsoSingle V).symm

instance : Full (single‚ÇÄ V) :=
  Full.ofIso (single‚ÇÄIsoSingle V).symm

end CochainComplex
