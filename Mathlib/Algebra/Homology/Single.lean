/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Algebra.Homology.HomologicalComplex

#align_import algebra.homology.single from "leanprover-community/mathlib"@"324a7502510e835cdbd3de1519b6c66b51fb2467"

/-!
# Chain complexes supported in a single degree

We define `single V j c : V â¥¤ HomologicalComplex V c`,
which constructs complexes in `V` of shape `c`, supported in degree `j`.

-/

open CategoryTheory Category Limits ZeroObject

universe v u

variable (V : Type u) [Category.{v} V] [HasZeroMorphisms V] [HasZeroObject V]

namespace HomologicalComplex

variable {Î¹ : Type*} [DecidableEq Î¹] (c : ComplexShape Î¹)

/-- The functor `V â¥¤ HomologicalComplex V c` creating a chain complex supported in a single degree.
-/
noncomputable def single (j : Î¹) : V â¥¤ HomologicalComplex V c where
  obj A :=
    { X := fun i => if i = j then A else 0
      d := fun i j => 0 }
  map f :=
    { f := fun i => if h : i = j then eqToHom (by dsimp; rw [if_pos h]) â‰« f â‰«
              eqToHom (by dsimp; rw [if_pos h]) else 0 }
  map_id A := by
    ext
    dsimp
    split_ifs with h
    Â· subst h
      simp
    Â· rw [if_neg h]
      simp
  map_comp f g := by
    ext
    dsimp
    split_ifs with h
    Â· subst h
      simp
    Â· simp
#align homological_complex.single HomologicalComplex.single

/-- The functor `V â¥¤ ChainComplex V â„•` creating a chain complex supported in degree zero. -/
noncomputable abbrev _root_.ChainComplex.singleâ‚€ : V â¥¤ ChainComplex V â„• :=
  single V (ComplexShape.down â„•) 0

/-- The functor `V â¥¤ CochainComplex V â„•` creating a cochain complex supported in degree zero. -/
noncomputable abbrev _root_.CochainComplex.singleâ‚€ : V â¥¤ CochainComplex V â„• :=
  single V (ComplexShape.up â„•) 0

variable {V}

@[simp]
lemma single_obj_X_self (j : Î¹) (A : V) :
    ((single V c j).obj A).X j = A := if_pos rfl

lemma isZero_single_obj_X (j : Î¹) (A : V) (i : Î¹) (hi : i â‰  j) :
    IsZero (((single V c j).obj A).X i) := by
  dsimp [single]
  rw [if_neg hi]
  exact Limits.isZero_zero V

/-- The object in degree `i` of `(single V c h).obj A` is just `A` when `i = j`. -/
noncomputable def singleObjXIsoOfEq (j : Î¹) (A : V) (i : Î¹) (hi : i = j) :
    ((single V c j).obj A).X i â‰… A :=
  eqToIso (by subst hi; simp [single])

/-- The object in degree `j` of `(single V c h).obj A` is just `A`. -/
noncomputable def singleObjXSelf (j : Î¹) (A : V) : ((single V c j).obj A).X j â‰… A :=
  singleObjXIsoOfEq c j A j rfl
set_option linter.uppercaseLean3 false in
#align homological_complex.single_obj_X_self HomologicalComplex.singleObjXSelf

@[simp]
lemma single_obj_d (j : Î¹) (A : V) (k l : Î¹) :
    ((single V c j).obj A).d k l = 0 := rfl

@[reassoc]
theorem single_map_f_self (j : Î¹) {A B : V} (f : A âŸ¶ B) :
    ((single V c j).map f).f j = (singleObjXSelf c j A).hom â‰«
      f â‰« (singleObjXSelf c j B).inv := by
  dsimp [single]
  rw [dif_pos rfl]
  rfl
#align homological_complex.single_map_f_self HomologicalComplex.single_map_f_self

@[ext]
lemma from_single_hom_ext {K : HomologicalComplex V c} {j : Î¹} {A : V}
    {f g : (single V c j).obj A âŸ¶ K} (hfg : f.f j = g.f j) : f = g := by
  ext i
  by_cases i = j
  Â· subst h
    exact hfg
  Â· apply (isZero_single_obj_X c j A i h).eq_of_src

@[ext]
lemma to_single_hom_ext {K : HomologicalComplex V c} {j : Î¹} {A : V}
    {f g : K âŸ¶ (single V c j).obj A} (hfg : f.f j = g.f j) : f = g := by
  ext i
  by_cases i = j
  Â· subst h
    exact hfg
  Â· apply (isZero_single_obj_X c j A i h).eq_of_tgt

instance (j : Î¹) : Faithful (single V c j) where
  map_injective {A B f g} w := by
    rw [â† cancel_mono (singleObjXSelf c j B).inv,
      â† cancel_epi (singleObjXSelf c j A).hom, â† single_map_f_self,
      â† single_map_f_self, w]

noncomputable instance (j : Î¹) : Full (single V c j) where
  preimage {A B} f := (singleObjXSelf c j A).inv â‰« f.f j â‰« (singleObjXSelf c j B).hom
  witness f := by
    ext
    simp [single_map_f_self]

variable {c}

/-- Constructor for morphisms to a single homological complex. -/
noncomputable def mkHomToSingle {K : HomologicalComplex V c} {j : Î¹} {A : V} (Ï† : K.X j âŸ¶ A)
    (hÏ† : âˆ€ (i : Î¹), c.Rel i j â†’ K.d i j â‰« Ï† = 0) :
    K âŸ¶ (single V c j).obj A where
  f i :=
    if hi : i = j
      then (K.XIsoOfEq hi).hom â‰« Ï† â‰« (singleObjXIsoOfEq c j A i hi).inv
      else 0
  comm' i k hik := by
    dsimp
    rw [comp_zero]
    split_ifs with hk
    Â· subst hk
      simp only [XIsoOfEq_rfl, Iso.refl_hom, id_comp, reassoc_of% hÏ† i hik, zero_comp]
    Â· apply (isZero_single_obj_X c j A k hk).eq_of_tgt

@[simp]
lemma mkHomToSingle_f {K : HomologicalComplex V c} {j : Î¹} {A : V} (Ï† : K.X j âŸ¶ A)
    (hÏ† : âˆ€ (i : Î¹), c.Rel i j â†’ K.d i j â‰« Ï† = 0) :
    (mkHomToSingle Ï† hÏ†).f j = Ï† â‰« (singleObjXSelf c j A).inv := by
  dsimp [mkHomToSingle]
  rw [dif_pos rfl, id_comp]
  rfl

/-- Constructor for morphisms from a single homological complex. -/
noncomputable def mkHomFromSingle {K : HomologicalComplex V c} {j : Î¹} {A : V} (Ï† : A âŸ¶ K.X j)
    (hÏ† : âˆ€ (k : Î¹), c.Rel j k â†’ Ï† â‰« K.d j k = 0) :
    (single V c j).obj A âŸ¶ K where
  f i :=
    if hi : i = j
      then (singleObjXIsoOfEq c j A i hi).hom â‰« Ï† â‰« (K.XIsoOfEq hi).inv
      else 0
  comm' i k hik := by
    dsimp
    rw [zero_comp]
    split_ifs with hi
    Â· subst hi
      simp only [XIsoOfEq_rfl, Iso.refl_inv, comp_id, assoc, hÏ† k hik, comp_zero]
    Â· apply (isZero_single_obj_X c j A i hi).eq_of_src

@[simp]
lemma mkHomFromSingle_f {K : HomologicalComplex V c} {j : Î¹} {A : V} (Ï† : A âŸ¶ K.X j)
    (hÏ† : âˆ€ (k : Î¹), c.Rel j k â†’ Ï† â‰« K.d j k = 0) :
    (mkHomFromSingle Ï† hÏ†).f j = (singleObjXSelf c j A).hom â‰« Ï† := by
  dsimp [mkHomFromSingle]
  rw [dif_pos rfl, comp_id]
  rfl

end HomologicalComplex

variable {V}

namespace ChainComplex

@[simp]
lemma singleâ‚€ObjXSelf (X : V) :
    HomologicalComplex.singleObjXSelf (ComplexShape.down â„•) 0 X = Iso.refl _ := rfl

/-- Morphisms from an `â„•`-indexed chain complex `C`
to a single object chain complex with `X` concentrated in degree 0
are the same as morphisms `f : C.X 0 âŸ¶ X` such that `C.d 1 0 â‰« f = 0`.
-/
@[simps]
noncomputable def toSingleâ‚€Equiv (C : ChainComplex V â„•) (X : V) :
    (C âŸ¶ (singleâ‚€ V).obj X) â‰ƒ { f : C.X 0 âŸ¶ X // C.d 1 0 â‰« f = 0 } where
  toFun Ï† := âŸ¨Ï†.f 0, by rw [â† Ï†.comm 1 0, HomologicalComplex.single_obj_d, comp_zero]âŸ©
  invFun f := HomologicalComplex.mkHomToSingle f.1 (fun i hi => by
    obtain rfl : i = 1 := by simpa using hi.symm
    exact f.2)
  left_inv Ï† := by
    ext
    dsimp
    simp only [HomologicalComplex.mkHomToSingle_f, singleâ‚€ObjXSelf, Iso.refl_inv]
    erw [comp_id]
  right_inv f := by
    ext
    simp only [HomologicalComplex.mkHomToSingle_f, singleâ‚€ObjXSelf, Iso.refl_inv]
    erw [comp_id]

end ChainComplex

namespace CochainComplex

@[simp]
lemma singleâ‚€ObjXSelf (X : V) :
    HomologicalComplex.singleObjXSelf (ComplexShape.up â„•) 0 X = Iso.refl _ := rfl

/-- Morphisms from a single object cochain complex with `X` concentrated in degree 0
to an `â„•`-indexed cochain complex `C`
are the same as morphisms `f : X âŸ¶ C.X 0` such that `f â‰« C.d 0 1 = 0`. -/
noncomputable def fromSingleâ‚€Equiv (C : CochainComplex V â„•) (X : V) :
    ((singleâ‚€ V).obj X âŸ¶ C) â‰ƒ { f : X âŸ¶ C.X 0 // f â‰« C.d 0 1 = 0 } where
  toFun Ï† := âŸ¨Ï†.f 0, by rw [Ï†.comm 0 1, HomologicalComplex.single_obj_d, zero_comp]âŸ©
  invFun f := HomologicalComplex.mkHomFromSingle f.1 (fun i hi => by
    obtain rfl : i = 1 := by simpa using hi.symm
    exact f.2)
  left_inv Ï† := by aesop_cat
  right_inv := by aesop_cat

end CochainComplex

/-

namespace ChainComplex

variable [HasEqualizers V] [HasCokernels V] [HasImages V] [HasImageMaps V]

/-- Sending objects to chain complexes supported at `0` then taking `0`-th homology
is the same as doing nothing.
-/
noncomputable def homology'Functor0Singleâ‚€ : singleâ‚€ V â‹™ homology'Functor V _ 0 â‰… ðŸ­ V :=
  NatIso.ofComponents (fun X => homology'.congr _ _ (by simp) (by simp) â‰ªâ‰« homology'ZeroZero)
    fun f => by
      -- Porting note: why can't `aesop_cat` do this?
      dsimp
      ext
      simp
#align chain_complex.homology_functor_0_singleâ‚€ ChainComplex.homology'Functor0Singleâ‚€

/-- Sending objects to chain complexes supported at `0` then taking `(n+1)`-st homology
is the same as the zero functor.
-/
noncomputable def homology'FunctorSuccSingleâ‚€ (n : â„•) :
    singleâ‚€ V â‹™ homology'Functor V _ (n + 1) â‰… 0 :=
  NatIso.ofComponents
    (fun X =>
      homology'.congr _ _ (by simp) (by simp) â‰ªâ‰«
        homology'ZeroZero â‰ªâ‰« (Functor.zero_obj _).isoZero.symm)
    fun f => (Functor.zero_obj _).eq_of_tgt _ _
#align chain_complex.homology_functor_succ_singleâ‚€ ChainComplex.homology'FunctorSuccSingleâ‚€

end

namespace CochainComplex

variable [HasEqualizers V] [HasCokernels V] [HasImages V] [HasImageMaps V]

/-- Sending objects to cochain complexes supported at `0` then taking `0`-th homology
is the same as doing nothing.
-/
noncomputable def homologyFunctor0Singleâ‚€ : singleâ‚€ V â‹™ homology'Functor V _ 0 â‰… ðŸ­ V :=
  NatIso.ofComponents (fun X => homology'.congr _ _ (by simp) (by simp) â‰ªâ‰« homology'ZeroZero)
    fun f => by
      -- Porting note: why can't `aesop_cat` do this?
      dsimp
      ext
      simp
#align cochain_complex.homology_functor_0_singleâ‚€ CochainComplex.homologyFunctor0Singleâ‚€

/-- Sending objects to cochain complexes supported at `0` then taking `(n+1)`-st homology
is the same as the zero functor.
-/
noncomputable def homology'FunctorSuccSingleâ‚€ (n : â„•) :
    singleâ‚€ V â‹™ homology'Functor V _ (n + 1) â‰… 0 :=
  NatIso.ofComponents
    (fun X =>
      homology'.congr _ _ (by simp) (by simp) â‰ªâ‰«
        homology'ZeroZero â‰ªâ‰« (Functor.zero_obj _).isoZero.symm)
    fun f => (Functor.zero_obj _).eq_of_tgt _ _
#align cochain_complex.homology_functor_succ_singleâ‚€ CochainComplex.homology'FunctorSuccSingleâ‚€

end CochainComplex

-/
