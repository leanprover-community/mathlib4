/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Subobject.Limits

#align_import algebra.homology.image_to_kernel from "leanprover-community/mathlib"@"618ea3d5c99240cd7000d8376924906a148bf9ff"

/-!
# Image-to-kernel comparison maps

Whenever `f : A âŸ¶ B` and `g : B âŸ¶ C` satisfy `w : f â‰« g = 0`,
we have `image_le_kernel f g w : imageSubobject f â‰¤ kernelSubobject g`
(assuming the appropriate images and kernels exist).

`imageToKernel f g w` is the corresponding morphism between objects in `C`.

We define `homology' f g w` of such a pair as the cokernel of `imageToKernel f g w`.

Note: As part of the transition to the new homology API, `homology` is temporarily
renamed `homology'`. It is planned that this definition shall be removed and replaced by
`ShortComplex.homology`.

-/

universe v u w

open CategoryTheory CategoryTheory.Limits

variable {Î¹ : Type*}
variable {V : Type u} [Category.{v} V] [HasZeroMorphisms V]

open scoped Classical

noncomputable section

section

variable {A B C : V} (f : A âŸ¶ B) [HasImage f] (g : B âŸ¶ C) [HasKernel g]

theorem image_le_kernel (w : f â‰« g = 0) : imageSubobject f â‰¤ kernelSubobject g :=
  imageSubobject_le_mk _ _ (kernel.lift _ _ w) (by simp)
#align image_le_kernel image_le_kernel

/-- The canonical morphism `imageSubobject f âŸ¶ kernelSubobject g` when `f â‰« g = 0`.
-/
def imageToKernel (w : f â‰« g = 0) : (imageSubobject f : V) âŸ¶ (kernelSubobject g : V) :=
  Subobject.ofLE _ _ (image_le_kernel _ _ w)
#align image_to_kernel imageToKernel

instance (w : f â‰« g = 0) : Mono (imageToKernel f g w) := by
  dsimp only [imageToKernel]
  infer_instance

/-- Prefer `imageToKernel`. -/
@[simp]
theorem subobject_ofLE_as_imageToKernel (w : f â‰« g = 0) (h) :
    Subobject.ofLE (imageSubobject f) (kernelSubobject g) h = imageToKernel f g w :=
  rfl
#align subobject_of_le_as_image_to_kernel subobject_ofLE_as_imageToKernel

attribute [local instance] ConcreteCategory.instFunLike

-- Porting note: removed elementwise attribute which does not seem to be helpful here
-- a more suitable lemma is added below
@[reassoc (attr := simp)]
theorem imageToKernel_arrow (w : f â‰« g = 0) :
    imageToKernel f g w â‰« (kernelSubobject g).arrow = (imageSubobject f).arrow := by
  simp [imageToKernel]
#align image_to_kernel_arrow imageToKernel_arrow

@[simp]
lemma imageToKernel_arrow_apply [ConcreteCategory V] (w : f â‰« g = 0)
    (x : (forget V).obj (Subobject.underlying.obj (imageSubobject f))) :
    (kernelSubobject g).arrow (imageToKernel f g w x) =
      (imageSubobject f).arrow x := by
  rw [â† comp_apply, imageToKernel_arrow]

-- This is less useful as a `simp` lemma than it initially appears,
-- as it "loses" the information the morphism factors through the image.
theorem factorThruImageSubobject_comp_imageToKernel (w : f â‰« g = 0) :
    factorThruImageSubobject f â‰« imageToKernel f g w = factorThruKernelSubobject g f w := by
  ext
  simp
#align factor_thru_image_subobject_comp_image_to_kernel factorThruImageSubobject_comp_imageToKernel

end

section

variable {A B C : V} (f : A âŸ¶ B) (g : B âŸ¶ C)

@[simp]
theorem imageToKernel_zero_left [HasKernels V] [HasZeroObject V] {w} :
    imageToKernel (0 : A âŸ¶ B) g w = 0 := by
  ext
  simp
#align image_to_kernel_zero_left imageToKernel_zero_left

theorem imageToKernel_zero_right [HasImages V] {w} :
    imageToKernel f (0 : B âŸ¶ C) w =
      (imageSubobject f).arrow â‰« inv (kernelSubobject (0 : B âŸ¶ C)).arrow := by
  ext
  simp
#align image_to_kernel_zero_right imageToKernel_zero_right

section

variable [HasKernels V] [HasImages V]

theorem imageToKernel_comp_right {D : V} (h : C âŸ¶ D) (w : f â‰« g = 0) :
    imageToKernel f (g â‰« h) (by simp [reassoc_of% w]) =
      imageToKernel f g w â‰« Subobject.ofLE _ _ (kernelSubobject_comp_le g h) := by
  ext
  simp
#align image_to_kernel_comp_right imageToKernel_comp_right

theorem imageToKernel_comp_left {Z : V} (h : Z âŸ¶ A) (w : f â‰« g = 0) :
    imageToKernel (h â‰« f) g (by simp [w]) =
      Subobject.ofLE _ _ (imageSubobject_comp_le h f) â‰« imageToKernel f g w := by
  ext
  simp
#align image_to_kernel_comp_left imageToKernel_comp_left

@[simp]
theorem imageToKernel_comp_mono {D : V} (h : C âŸ¶ D) [Mono h] (w) :
    imageToKernel f (g â‰« h) w =
      imageToKernel f g ((cancel_mono h).mp (by simpa using w : (f â‰« g) â‰« h = 0 â‰« h)) â‰«
        (Subobject.isoOfEq _ _ (kernelSubobject_comp_mono g h)).inv := by
  ext
  simp
#align image_to_kernel_comp_mono imageToKernel_comp_mono

@[simp]
theorem imageToKernel_epi_comp {Z : V} (h : Z âŸ¶ A) [Epi h] (w) :
    imageToKernel (h â‰« f) g w =
      Subobject.ofLE _ _ (imageSubobject_comp_le h f) â‰«
        imageToKernel f g ((cancel_epi h).mp (by simpa using w : h â‰« f â‰« g = h â‰« 0)) := by
  ext
  simp
#align image_to_kernel_epi_comp imageToKernel_epi_comp

end

@[simp]
theorem imageToKernel_comp_hom_inv_comp [HasEqualizers V] [HasImages V] {Z : V} {i : B â‰… Z} (w) :
    imageToKernel (f â‰« i.hom) (i.inv â‰« g) w =
      (imageSubobjectCompIso _ _).hom â‰«
        imageToKernel f g (by simpa using w) â‰« (kernelSubobjectIsoComp i.inv g).inv := by
  ext
  simp
#align image_to_kernel_comp_hom_inv_comp imageToKernel_comp_hom_inv_comp

open ZeroObject

/-- `imageToKernel` for `A --0--> B --g--> C`, where `g` is a mono is itself an epi
(i.e. the sequence is exact at `B`).
-/
instance imageToKernel_epi_of_zero_of_mono [HasKernels V] [HasZeroObject V] [Mono g] :
    Epi (imageToKernel (0 : A âŸ¶ B) g (by simp)) :=
  epi_of_target_iso_zero _ (kernelSubobjectIso g â‰ªâ‰« kernel.ofMono g)
#align image_to_kernel_epi_of_zero_of_mono imageToKernel_epi_of_zero_of_mono

/-- `imageToKernel` for `A --f--> B --0--> C`, where `g` is an epi is itself an epi
(i.e. the sequence is exact at `B`).
-/
instance imageToKernel_epi_of_epi_of_zero [HasImages V] [Epi f] :
    Epi (imageToKernel f (0 : B âŸ¶ C) (by simp)) := by
  simp only [imageToKernel_zero_right]
  haveI := epi_image_of_epi f
  rw [â† imageSubobject_arrow]
  exact @epi_comp _ _ _ _ _ _ (epi_comp _ _) _ _
#align image_to_kernel_epi_of_epi_of_zero imageToKernel_epi_of_epi_of_zero

end

section

variable {A B C : V} (f : A âŸ¶ B) [HasImage f] (g : B âŸ¶ C) [HasKernel g]

/-- The homology of a pair of morphisms `f : A âŸ¶ B` and `g : B âŸ¶ C` satisfying `f â‰« g = 0`
is the cokernel of the `imageToKernel` morphism for `f` and `g`.
-/
def homology' {A B C : V} (f : A âŸ¶ B) [HasImage f] (g : B âŸ¶ C) [HasKernel g] (w : f â‰« g = 0)
    [HasCokernel (imageToKernel f g w)] : V :=
  cokernel (imageToKernel f g w)
#align homology homology'

section

variable (w : f â‰« g = 0) [HasCokernel (imageToKernel f g w)]

/-- The morphism from cycles to homology. -/
def homology'.Ï€ : (kernelSubobject g : V) âŸ¶ homology' f g w :=
  cokernel.Ï€ _
#align homology.Ï€ homology'.Ï€

@[simp]
theorem homology'.condition : imageToKernel f g w â‰« homology'.Ï€ f g w = 0 :=
  cokernel.condition _
#align homology.condition homology'.condition

/-- To construct a map out of homology, it suffices to construct a map out of the cycles
which vanishes on boundaries.
-/
def homology'.desc {D : V} (k : (kernelSubobject g : V) âŸ¶ D) (p : imageToKernel f g w â‰« k = 0) :
    homology' f g w âŸ¶ D :=
  cokernel.desc _ k p
#align homology.desc homology'.desc

-- Porting note: removed elementwise attribute which does not seem to be helpful here
@[reassoc (attr := simp)]
theorem homology'.Ï€_desc {D : V} (k : (kernelSubobject g : V) âŸ¶ D)
    (p : imageToKernel f g w â‰« k = 0) : homology'.Ï€ f g w â‰« homology'.desc f g w k p = k := by
  simp [homology'.Ï€, homology'.desc]
#align homology.Ï€_desc homology'.Ï€_desc

/-- To check two morphisms out of `homology' f g w` are equal, it suffices to check on cycles. -/
@[ext]
theorem homology'.ext {D : V} {k k' : homology' f g w âŸ¶ D}
    (p : homology'.Ï€ f g w â‰« k = homology'.Ï€ f g w â‰« k') : k = k' :=
  coequalizer.hom_ext p
#align homology.ext homology'.ext

/-- The cokernel of the map `Im f âŸ¶ Ker 0` is isomorphic to the cokernel of `f.` -/
def homology'OfZeroRight [HasCokernel (imageToKernel f (0 : B âŸ¶ C) comp_zero)] [HasCokernel f]
    [HasCokernel (image.Î¹ f)] [Epi (factorThruImage f)] :
    homology' f (0 : B âŸ¶ C) comp_zero â‰… cokernel f :=
  (cokernel.mapIso _ _ (imageSubobjectIso _) ((kernelSubobjectIso 0).trans kernelZeroIsoSource)
        (by simp)).trans
    (cokernelImageÎ¹ _)
#align homology_of_zero_right homology'OfZeroRight

/-- The kernel of the map `Im 0 âŸ¶ Ker f` is isomorphic to the kernel of `f.` -/
def homology'OfZeroLeft [HasZeroObject V] [HasKernels V] [HasImage (0 : A âŸ¶ B)]
    [HasCokernel (imageToKernel (0 : A âŸ¶ B) g zero_comp)] :
    homology' (0 : A âŸ¶ B) g zero_comp â‰… kernel g :=
  ((cokernelIsoOfEq <| imageToKernel_zero_left _).trans cokernelZeroIsoTarget).trans
    (kernelSubobjectIso _)
#align homology_of_zero_left homology'OfZeroLeft

/-- `homology 0 0 _` is just the middle object. -/
@[simps]
def homology'ZeroZero [HasZeroObject V] [HasImage (0 : A âŸ¶ B)]
    [HasCokernel (imageToKernel (0 : A âŸ¶ B) (0 : B âŸ¶ C) zero_comp)] :
    homology' (0 : A âŸ¶ B) (0 : B âŸ¶ C) zero_comp â‰… B where
  hom := homology'.desc (0 : A âŸ¶ B) (0 : B âŸ¶ C) zero_comp (kernelSubobject 0).arrow (by simp)
  inv := inv (kernelSubobject 0).arrow â‰« homology'.Ï€ _ _ _
#align homology_zero_zero homology'ZeroZero

end

section

variable {f g} (w : f â‰« g = 0) {A' B' C' : V} {f' : A' âŸ¶ B'} [HasImage f'] {g' : B' âŸ¶ C'}
  [HasKernel g'] (w' : f' â‰« g' = 0) (Î± : Arrow.mk f âŸ¶ Arrow.mk f') [HasImageMap Î±]
  (Î² : Arrow.mk g âŸ¶ Arrow.mk g') {Aâ‚ Bâ‚ Câ‚ : V} {fâ‚ : Aâ‚ âŸ¶ Bâ‚} [HasImage fâ‚] {gâ‚ : Bâ‚ âŸ¶ Câ‚}
  [HasKernel gâ‚] (wâ‚ : fâ‚ â‰« gâ‚ = 0) {Aâ‚‚ Bâ‚‚ Câ‚‚ : V} {fâ‚‚ : Aâ‚‚ âŸ¶ Bâ‚‚} [HasImage fâ‚‚] {gâ‚‚ : Bâ‚‚ âŸ¶ Câ‚‚}
  [HasKernel gâ‚‚] (wâ‚‚ : fâ‚‚ â‰« gâ‚‚ = 0) {Aâ‚ƒ Bâ‚ƒ Câ‚ƒ : V} {fâ‚ƒ : Aâ‚ƒ âŸ¶ Bâ‚ƒ} [HasImage fâ‚ƒ] {gâ‚ƒ : Bâ‚ƒ âŸ¶ Câ‚ƒ}
  [HasKernel gâ‚ƒ] (wâ‚ƒ : fâ‚ƒ â‰« gâ‚ƒ = 0) (Î±â‚ : Arrow.mk fâ‚ âŸ¶ Arrow.mk fâ‚‚) [HasImageMap Î±â‚]
  (Î²â‚ : Arrow.mk gâ‚ âŸ¶ Arrow.mk gâ‚‚) (Î±â‚‚ : Arrow.mk fâ‚‚ âŸ¶ Arrow.mk fâ‚ƒ) [HasImageMap Î±â‚‚]
  (Î²â‚‚ : Arrow.mk gâ‚‚ âŸ¶ Arrow.mk gâ‚ƒ)

/-- Given compatible commutative squares between
a pair `f g` and a pair `f' g'` satisfying `f â‰« g = 0` and `f' â‰« g' = 0`,
the `imageToKernel` morphisms intertwine the induced map on kernels and the induced map on images.
-/
@[reassoc]
theorem imageSubobjectMap_comp_imageToKernel (p : Î±.right = Î².left) :
    imageToKernel f g w â‰« kernelSubobjectMap Î² = imageSubobjectMap Î± â‰« imageToKernel f' g' w' := by
  ext
  simp [p]
#align image_subobject_map_comp_image_to_kernel imageSubobjectMap_comp_imageToKernel

variable [HasCokernel (imageToKernel f g w)] [HasCokernel (imageToKernel f' g' w')]
variable [HasCokernel (imageToKernel fâ‚ gâ‚ wâ‚)]
variable [HasCokernel (imageToKernel fâ‚‚ gâ‚‚ wâ‚‚)]
variable [HasCokernel (imageToKernel fâ‚ƒ gâ‚ƒ wâ‚ƒ)]

/-- Given compatible commutative squares between
a pair `f g` and a pair `f' g'` satisfying `f â‰« g = 0` and `f' â‰« g' = 0`,
we get a morphism on homology.
-/
def homology'.map (p : Î±.right = Î².left) : homology' f g w âŸ¶ homology' f' g' w' :=
  cokernel.desc _ (kernelSubobjectMap Î² â‰« cokernel.Ï€ _) <| by
    rw [imageSubobjectMap_comp_imageToKernel_assoc w w' Î± Î² p]
    simp only [cokernel.condition, comp_zero]
#align homology.map homology'.map

-- Porting note: removed elementwise attribute which does not seem to be helpful here,
-- the correct lemma is stated below
@[reassoc (attr := simp)]
theorem homology'.Ï€_map (p : Î±.right = Î².left) :
    homology'.Ï€ f g w â‰« homology'.map w w' Î± Î² p =
      kernelSubobjectMap Î² â‰« homology'.Ï€ f' g' w' := by
  simp only [homology'.Ï€, homology'.map, cokernel.Ï€_desc]
#align homology.Ï€_map homology'.Ï€_map

section

attribute [local instance] ConcreteCategory.instFunLike

@[simp]
lemma homology'.Ï€_map_apply [ConcreteCategory.{w} V] (p : Î±.right = Î².left)
    (x : (forget V).obj (Subobject.underlying.obj (kernelSubobject g))) :
    homology'.map w w' Î± Î² p (homology'.Ï€ f g w x) =
      homology'.Ï€ f' g' w' (kernelSubobjectMap Î² x) := by
  simp only [â† comp_apply, homology'.Ï€_map w w' Î± Î² p]

end

@[reassoc (attr := simp), elementwise (attr := simp)]
theorem homology'.map_desc (p : Î±.right = Î².left) {D : V} (k : (kernelSubobject g' : V) âŸ¶ D)
    (z : imageToKernel f' g' w' â‰« k = 0) :
    homology'.map w w' Î± Î² p â‰« homology'.desc f' g' w' k z =
      homology'.desc f g w (kernelSubobjectMap Î² â‰« k)
        (by simp only [imageSubobjectMap_comp_imageToKernel_assoc w w' Î± Î² p, z, comp_zero]) := by
  ext
  simp only [homology'.Ï€_desc, homology'.Ï€_map_assoc]
#align homology.map_desc homology'.map_desc

@[simp]
theorem homology'.map_id : homology'.map w w (ğŸ™ _) (ğŸ™ _) rfl = ğŸ™ _ := by
  ext
  simp only [homology'.Ï€_map, kernelSubobjectMap_id, Category.id_comp, Category.comp_id]
#align homology.map_id homology'.map_id

/-- Auxiliary lemma for homology computations. -/
theorem homology'.comp_right_eq_comp_left {V : Type*} [Category V] {Aâ‚ Bâ‚ Câ‚ Aâ‚‚ Bâ‚‚ Câ‚‚ Aâ‚ƒ Bâ‚ƒ Câ‚ƒ : V}
    {fâ‚ : Aâ‚ âŸ¶ Bâ‚} {gâ‚ : Bâ‚ âŸ¶ Câ‚} {fâ‚‚ : Aâ‚‚ âŸ¶ Bâ‚‚} {gâ‚‚ : Bâ‚‚ âŸ¶ Câ‚‚} {fâ‚ƒ : Aâ‚ƒ âŸ¶ Bâ‚ƒ} {gâ‚ƒ : Bâ‚ƒ âŸ¶ Câ‚ƒ}
    {Î±â‚ : Arrow.mk fâ‚ âŸ¶ Arrow.mk fâ‚‚} {Î²â‚ : Arrow.mk gâ‚ âŸ¶ Arrow.mk gâ‚‚}
    {Î±â‚‚ : Arrow.mk fâ‚‚ âŸ¶ Arrow.mk fâ‚ƒ} {Î²â‚‚ : Arrow.mk gâ‚‚ âŸ¶ Arrow.mk gâ‚ƒ} (pâ‚ : Î±â‚.right = Î²â‚.left)
    (pâ‚‚ : Î±â‚‚.right = Î²â‚‚.left) : (Î±â‚ â‰« Î±â‚‚).right = (Î²â‚ â‰« Î²â‚‚).left := by
  simp only [Arrow.comp_left, Arrow.comp_right, pâ‚, pâ‚‚]
#align homology.comp_right_eq_comp_left homology'.comp_right_eq_comp_left

@[reassoc]
theorem homology'.map_comp (pâ‚ : Î±â‚.right = Î²â‚.left) (pâ‚‚ : Î±â‚‚.right = Î²â‚‚.left) :
    homology'.map wâ‚ wâ‚‚ Î±â‚ Î²â‚ pâ‚ â‰« homology'.map wâ‚‚ wâ‚ƒ Î±â‚‚ Î²â‚‚ pâ‚‚ =
      homology'.map wâ‚ wâ‚ƒ (Î±â‚ â‰« Î±â‚‚) (Î²â‚ â‰« Î²â‚‚) (homology'.comp_right_eq_comp_left pâ‚ pâ‚‚) := by
  ext
  simp only [kernelSubobjectMap_comp, homology'.Ï€_map_assoc, homology'.Ï€_map, Category.assoc]
#align homology.map_comp homology'.map_comp

/-- An isomorphism between two three-term complexes induces an isomorphism on homology. -/
def homology'.mapIso (Î± : Arrow.mk fâ‚ â‰… Arrow.mk fâ‚‚) (Î² : Arrow.mk gâ‚ â‰… Arrow.mk gâ‚‚)
    (p : Î±.hom.right = Î².hom.left) : homology' fâ‚ gâ‚ wâ‚ â‰… homology' fâ‚‚ gâ‚‚ wâ‚‚ where
  hom := homology'.map wâ‚ wâ‚‚ Î±.hom Î².hom p
  inv :=
    homology'.map wâ‚‚ wâ‚ Î±.inv Î².inv
      (by
        rw [â† cancel_mono Î±.hom.right, â† Comma.comp_right, Î±.inv_hom_id, Comma.id_right, p, â†
          Comma.comp_left, Î².inv_hom_id, Comma.id_left]
        rfl)
  hom_inv_id := by
    rw [homology'.map_comp, â† homology'.map_id]
    congr <;> simp only [Iso.hom_inv_id]
  inv_hom_id := by
    rw [homology'.map_comp, â† homology'.map_id]
    congr <;> simp only [Iso.inv_hom_id]
#align homology.map_iso homology'.mapIso

end

end

section

variable {A B C : V} {f : A âŸ¶ B} {g : B âŸ¶ C} (w : f â‰« g = 0) {f' : A âŸ¶ B} {g' : B âŸ¶ C}
  (w' : f' â‰« g' = 0) [HasKernels V] [HasCokernels V] [HasImages V] [HasImageMaps V]

-- Porting note: removed the private auxiliary tactic which becomes unnecessary
--/-- Custom tactic to golf and speedup boring proofs in `homology.congr`. -/
--private unsafe def aux_tac : tactic Unit :=
--  sorry

/-- `homology f g w â‰… homology f' g' w'` if `f = f'` and `g = g'`.
(Note the objects are not changing here.)
-/
@[simps]
def homology'.congr (pf : f = f') (pg : g = g') : homology' f g w â‰… homology' f' g' w' where
  hom := homology'.map w w' âŸ¨ğŸ™ _, ğŸ™ _, by aesop_catâŸ© âŸ¨ğŸ™ _, ğŸ™ _, by aesop_catâŸ© rfl
  inv := homology'.map w' w âŸ¨ğŸ™ _, ğŸ™ _, by aesop_catâŸ© âŸ¨ğŸ™ _, ğŸ™ _, by aesop_catâŸ© rfl
  hom_inv_id := by
    obtain rfl := pf
    obtain rfl := pg
    rw [homology'.map_comp, â† homology'.map_id]
    congr <;> aesop_cat
  inv_hom_id := by
    obtain rfl := pf
    obtain rfl := pg
    rw [homology'.map_comp, â† homology'.map_id]
    congr <;> aesop_cat
#align homology.congr homology'.congr

end

/-!
We provide a variant `imageToKernel' : image f âŸ¶ kernel g`,
and use this to give alternative formulas for `homology f g w`.
-/


section imageToKernel'

variable {A B C : V} (f : A âŸ¶ B) (g : B âŸ¶ C) (w : f â‰« g = 0) [HasKernels V] [HasImages V]

/-- While `imageToKernel f g w` provides a morphism
`imageSubobject f âŸ¶ kernelSubobject g`
in terms of the subobject API,
this variant provides a morphism
`image f âŸ¶ kernel g`,
which is sometimes more convenient.
-/
def imageToKernel' (w : f â‰« g = 0) : image f âŸ¶ kernel g :=
  kernel.lift g (image.Î¹ f) <| by
    ext
    simpa using w
#align image_to_kernel' imageToKernel'

@[simp]
theorem imageSubobjectIso_imageToKernel' (w : f â‰« g = 0) :
    (imageSubobjectIso f).hom â‰« imageToKernel' f g w =
      imageToKernel f g w â‰« (kernelSubobjectIso g).hom := by
  ext
  simp [imageToKernel']
#align image_subobject_iso_image_to_kernel' imageSubobjectIso_imageToKernel'

@[simp]
theorem imageToKernel'_kernelSubobjectIso (w : f â‰« g = 0) :
    imageToKernel' f g w â‰« (kernelSubobjectIso g).inv =
      (imageSubobjectIso f).inv â‰« imageToKernel f g w := by
  ext
  simp [imageToKernel']
#align image_to_kernel'_kernel_subobject_iso imageToKernel'_kernelSubobjectIso

variable [HasCokernels V]

/-- `homology' f g w` can be computed as the cokernel of `imageToKernel' f g w`.
-/
def homology'IsoCokernelImageToKernel' (w : f â‰« g = 0) :
    homology' f g w â‰… cokernel (imageToKernel' f g w) where
  hom := cokernel.map _ _ (imageSubobjectIso f).hom (kernelSubobjectIso g).hom
      (by simp only [imageSubobjectIso_imageToKernel'])
  inv := cokernel.map _ _ (imageSubobjectIso f).inv (kernelSubobjectIso g).inv
      (by simp only [imageToKernel'_kernelSubobjectIso])
  hom_inv_id := by
    -- Just calling `ext` here uses the higher priority `homology'.ext`,
    -- which precomposes with `homology'.Ï€`.
    -- As we are trying to work in terms of `cokernel`, it is better to use `coequalizer.hom_ext`.
    apply coequalizer.hom_ext
    simp only [Iso.hom_inv_id_assoc, cokernel.Ï€_desc, cokernel.Ï€_desc_assoc, Category.assoc,
      coequalizer_as_cokernel]
    exact (Category.comp_id _).symm
  inv_hom_id := by
    ext
    simp only [Iso.inv_hom_id_assoc, cokernel.Ï€_desc, Category.comp_id, cokernel.Ï€_desc_assoc,
      Category.assoc]
#align homology_iso_cokernel_image_to_kernel' homology'IsoCokernelImageToKernel'

variable [HasEqualizers V]

/-- `homology f g w` can be computed as the cokernel of `kernel.lift g f w`.
-/
def homology'IsoCokernelLift (w : f â‰« g = 0) : homology' f g w â‰… cokernel (kernel.lift g f w) := by
  refine homology'IsoCokernelImageToKernel' f g w â‰ªâ‰« ?_
  have p : factorThruImage f â‰« imageToKernel' f g w = kernel.lift g f w := by
    ext
    simp [imageToKernel']
  exact (cokernelEpiComp _ _).symm â‰ªâ‰« cokernelIsoOfEq p
#align homology_iso_cokernel_lift homology'IsoCokernelLift

end imageToKernel'
