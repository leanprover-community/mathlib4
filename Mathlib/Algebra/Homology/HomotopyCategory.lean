/-
Copyright (c) 2021 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.Algebra.Homology.Homotopy
import Mathlib.Algebra.Homology.Linear
import Mathlib.CategoryTheory.MorphismProperty.IsInvertedBy
import Mathlib.CategoryTheory.Quotient.Linear
import Mathlib.CategoryTheory.Quotient.Preadditive

/-!
# The homotopy category

`HomotopyCategory V c` gives the category of chain complexes of shape `c` in `V`,
with chain maps identified when they are homotopic.
-/

universe v u

noncomputable section

open CategoryTheory CategoryTheory.Limits HomologicalComplex

variable {R : Type*} [Semiring R]
  {Œπ : Type*} (V : Type u) [Category.{v} V] [Preadditive V] (c : ComplexShape Œπ)

/-- The congruence on `HomologicalComplex V c` given by the existence of a homotopy.
-/
def homotopic : HomRel (HomologicalComplex V c) := fun _ _ f g => Nonempty (Homotopy f g)

instance homotopy_congruence : Congruence (homotopic V c) where
  equivalence :=
    { refl := fun C => ‚ü®Homotopy.refl C‚ü©
      symm := fun ‚ü®w‚ü© => ‚ü®w.symm‚ü©
      trans := fun ‚ü®w‚ÇÅ‚ü© ‚ü®w‚ÇÇ‚ü© => ‚ü®w‚ÇÅ.trans w‚ÇÇ‚ü© }
  compLeft := fun _ _ _ ‚ü®i‚ü© => ‚ü®i.compLeft _‚ü©
  compRight := fun _ ‚ü®i‚ü© => ‚ü®i.compRight _‚ü©

/-- `HomotopyCategory V c` is the category of chain complexes of shape `c` in `V`,
with chain maps identified when they are homotopic. -/
def HomotopyCategory :=
  CategoryTheory.Quotient (homotopic V c)

instance : Category (HomotopyCategory V c) := by
  dsimp only [HomotopyCategory]
  infer_instance

-- TODO the homotopy_category is preadditive
namespace HomotopyCategory

instance : Preadditive (HomotopyCategory V c) := Quotient.preadditive _ (by
  rintro _ _ _ _ _ _ ‚ü®h‚ü© ‚ü®h'‚ü©
  exact ‚ü®Homotopy.add h h'‚ü©)

/-- The quotient functor from complexes to the homotopy category. -/
def quotient : HomologicalComplex V c ‚•§ HomotopyCategory V c :=
  CategoryTheory.Quotient.functor _

instance : (quotient V c).Full := Quotient.full_functor _

instance : (quotient V c).EssSurj := Quotient.essSurj_functor _

instance : (quotient V c).Additive where

instance : Preadditive (CategoryTheory.Quotient (homotopic V c)) :=
  (inferInstance : Preadditive (HomotopyCategory V c))

instance : Functor.Additive (Quotient.functor (homotopic V c)) where

instance [Linear R V] : Linear R (HomotopyCategory V c) :=
  Quotient.linear R (homotopic V c) (fun _ _ _ _ _ h => ‚ü®h.some.smul _‚ü©)

instance [Linear R V] : Functor.Linear R (HomotopyCategory.quotient V c) :=
  Quotient.linear_functor _ _ _

open ZeroObject

instance [HasZeroObject V] : Inhabited (HomotopyCategory V c) :=
  ‚ü®(quotient V c).obj 0‚ü©

instance [HasZeroObject V] : HasZeroObject (HomotopyCategory V c) :=
  ‚ü®(quotient V c).obj 0, by
    rw [IsZero.iff_id_eq_zero, ‚Üê (quotient V c).map_id, id_zero, Functor.map_zero]‚ü©

instance {D : Type*} [Category D] : ((Functor.whiskeringLeft _ _ D).obj (quotient V c)).Full :=
  Quotient.full_whiskeringLeft_functor _ _

instance {D : Type*} [Category D] : ((Functor.whiskeringLeft _ _ D).obj (quotient V c)).Faithful :=
  Quotient.faithful_whiskeringLeft_functor _ _

variable {V c}

lemma quotient_obj_surjective (X : HomotopyCategory V c) :
    ‚àÉ (K : HomologicalComplex V c), (quotient _ _).obj K = X :=
  ‚ü®_, rfl‚ü©

-- Porting note: removed @[simp] attribute because it hinders the automatic application of the
-- more useful `quotient_map_out`
theorem quotient_obj_as (C : HomologicalComplex V c) : ((quotient V c).obj C).as = C :=
  rfl

@[simp]
theorem quotient_map_out {C D : HomotopyCategory V c} (f : C ‚ü∂ D) : (quotient V c).map f.out = f :=
  Quot.out_eq _

-- Porting note: added to ease the port
theorem quot_mk_eq_quotient_map {C D : HomologicalComplex V c} (f : C ‚ü∂ D) :
    Quot.mk _ f = (quotient V c).map f := rfl

theorem eq_of_homotopy {C D : HomologicalComplex V c} (f g : C ‚ü∂ D) (h : Homotopy f g) :
    (quotient V c).map f = (quotient V c).map g :=
  CategoryTheory.Quotient.sound _ ‚ü®h‚ü©

/-- If two chain maps become equal in the homotopy category, then they are homotopic. -/
def homotopyOfEq {C D : HomologicalComplex V c} (f g : C ‚ü∂ D)
    (w : (quotient V c).map f = (quotient V c).map g) : Homotopy f g :=
  ((Quotient.functor_map_eq_iff _ _ _).mp w).some

/-- An arbitrarily chosen representation of the image of a chain map in the homotopy category
is homotopic to the original chain map.
-/
def homotopyOutMap {C D : HomologicalComplex V c} (f : C ‚ü∂ D) :
    Homotopy ((quotient V c).map f).out f := by
  apply homotopyOfEq
  simp

theorem quotient_map_out_comp_out {C D E : HomotopyCategory V c} (f : C ‚ü∂ D) (g : D ‚ü∂ E) :
    (quotient V c).map (Quot.out f ‚â´ Quot.out g) = f ‚â´ g := by simp

/-- Homotopy equivalent complexes become isomorphic in the homotopy category. -/
@[simps]
def isoOfHomotopyEquiv {C D : HomologicalComplex V c} (f : HomotopyEquiv C D) :
    (quotient V c).obj C ‚âÖ (quotient V c).obj D where
  hom := (quotient V c).map f.hom
  inv := (quotient V c).map f.inv
  hom_inv_id := by
    rw [‚Üê (quotient V c).map_comp, ‚Üê (quotient V c).map_id]
    exact eq_of_homotopy _ _ f.homotopyHomInvId
  inv_hom_id := by
    rw [‚Üê (quotient V c).map_comp, ‚Üê (quotient V c).map_id]
    exact eq_of_homotopy _ _ f.homotopyInvHomId

/-- If two complexes become isomorphic in the homotopy category,
  then they were homotopy equivalent. -/
def homotopyEquivOfIso {C D : HomologicalComplex V c}
    (i : (quotient V c).obj C ‚âÖ (quotient V c).obj D) : HomotopyEquiv C D where
  hom := Quot.out i.hom
  inv := Quot.out i.inv
  homotopyHomInvId :=
    homotopyOfEq _ _
      (by rw [quotient_map_out_comp_out, i.hom_inv_id, (quotient V c).map_id])
  homotopyInvHomId :=
    homotopyOfEq _ _
      (by rw [quotient_map_out_comp_out, i.inv_hom_id, (quotient V c).map_id])

variable (V c) in
lemma quotient_inverts_homotopyEquivalences :
    (HomologicalComplex.homotopyEquivalences V c).IsInvertedBy (quotient V c) := by
  rintro K L _ ‚ü®e, rfl‚ü©
  change IsIso (isoOfHomotopyEquiv e).hom
  infer_instance

lemma isZero_quotient_obj_iff (C : HomologicalComplex V c) :
    IsZero ((quotient _ _).obj C) ‚Üî Nonempty (Homotopy (ùüô C) 0) := by
  rw [IsZero.iff_id_eq_zero]
  constructor
  ¬∑ intro h
    exact ‚ü®(homotopyOfEq _ _ (by simp [h]))‚ü©
  ¬∑ rintro ‚ü®h‚ü©
    simpa using (eq_of_homotopy _ _ h)

variable (V c)

section

variable [CategoryWithHomology V]

open Classical in
/-- The `i`-th homology, as a functor from the homotopy category. -/
noncomputable def homologyFunctor (i : Œπ) : HomotopyCategory V c ‚•§ V :=
  CategoryTheory.Quotient.lift _ (HomologicalComplex.homologyFunctor V c i) (by
    rintro K L f g ‚ü®h‚ü©
    exact h.homologyMap_eq i)

/-- The homology functor on the homotopy category is induced by
the homology functor on homological complexes. -/
noncomputable def homologyFunctorFactors (i : Œπ) :
    quotient V c ‚ãô homologyFunctor V c i ‚âÖ
      HomologicalComplex.homologyFunctor V c i :=
  Quotient.lift.isLift _ _ _

-- this is to prevent any abuse of defeq
attribute [irreducible] homologyFunctor homologyFunctorFactors

instance (i : Œπ) : (homologyFunctor V c i).Additive := by
  have := Functor.additive_of_iso (homologyFunctorFactors V c i).symm
  exact Functor.additive_of_full_essSurj_comp (quotient V c) _

end

end HomotopyCategory

namespace CategoryTheory

variable {V} {W : Type*} [Category W] [Preadditive W]

-- Porting note: given a simpler definition of this functor
/-- An additive functor induces a functor between homotopy categories. -/
@[simps! obj]
def Functor.mapHomotopyCategory (F : V ‚•§ W) [F.Additive] (c : ComplexShape Œπ) :
    HomotopyCategory V c ‚•§ HomotopyCategory W c :=
  CategoryTheory.Quotient.lift _ (F.mapHomologicalComplex c ‚ãô HomotopyCategory.quotient W c)
    (fun _ _ _ _ ‚ü®h‚ü© => HomotopyCategory.eq_of_homotopy _ _ (F.mapHomotopy h))

@[simp]
lemma Functor.mapHomotopyCategory_map (F : V ‚•§ W) [F.Additive] {c : ComplexShape Œπ}
    {K L : HomologicalComplex V c} (f : K ‚ü∂ L) :
    (F.mapHomotopyCategory c).map ((HomotopyCategory.quotient V c).map f) =
      (HomotopyCategory.quotient W c).map ((F.mapHomologicalComplex c).map f) :=
  rfl

/-- The obvious isomorphism between
`HomotopyCategory.quotient V c ‚ãô F.mapHomotopyCategory c` and
`F.mapHomologicalComplex c ‚ãô HomotopyCategory.quotient W c` when `F : V ‚•§ W` is
an additive functor. -/
def Functor.mapHomotopyCategoryFactors (F : V ‚•§ W) [F.Additive] (c : ComplexShape Œπ) :
    HomotopyCategory.quotient V c ‚ãô F.mapHomotopyCategory c ‚âÖ
      F.mapHomologicalComplex c ‚ãô HomotopyCategory.quotient W c :=
  CategoryTheory.Quotient.lift.isLift _ _ _

-- TODO `F.mapHomotopyCategory c` is additive (and linear when `F` is linear).
-- TODO develop lifting of natural transformations for general quotient categories so that
-- `NatTrans.mapHomotopyCategory` become a particular case of it
/-- A natural transformation induces a natural transformation between
  the induced functors on the homotopy category. -/
@[simps]
def NatTrans.mapHomotopyCategory {F G : V ‚•§ W} [F.Additive] [G.Additive] (Œ± : F ‚ü∂ G)
    (c : ComplexShape Œπ) : F.mapHomotopyCategory c ‚ü∂ G.mapHomotopyCategory c where
  app C := (HomotopyCategory.quotient W c).map ((NatTrans.mapHomologicalComplex Œ± c).app C.as)
  naturality := by
    rintro ‚ü®C‚ü© ‚ü®D‚ü© ‚ü®f : C ‚ü∂ D‚ü©
    simp only [HomotopyCategory.quot_mk_eq_quotient_map, Functor.mapHomotopyCategory_map,
      ‚Üê Functor.map_comp, NatTrans.naturality]

@[simp]
theorem NatTrans.mapHomotopyCategory_id (c : ComplexShape Œπ) (F : V ‚•§ W) [F.Additive] :
    NatTrans.mapHomotopyCategory (ùüô F) c = ùüô (F.mapHomotopyCategory c) := by cat_disch

@[simp]
theorem NatTrans.mapHomotopyCategory_comp (c : ComplexShape Œπ) {F G H : V ‚•§ W} [F.Additive]
    [G.Additive] [H.Additive] (Œ± : F ‚ü∂ G) (Œ≤ : G ‚ü∂ H) :
    NatTrans.mapHomotopyCategory (Œ± ‚â´ Œ≤) c =
      NatTrans.mapHomotopyCategory Œ± c ‚â´ NatTrans.mapHomotopyCategory Œ≤ c := by cat_disch

end CategoryTheory
