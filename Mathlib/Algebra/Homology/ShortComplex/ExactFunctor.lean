/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou, Jujian Zhang
-/
import Mathlib.Algebra.Homology.ShortComplex.PreservesHomology
import Mathlib.Algebra.Homology.ShortComplex.ShortExact
import Mathlib.Algebra.Homology.ShortComplex.Abelian
import Mathlib.CategoryTheory.Preadditive.LeftExact
import Mathlib.CategoryTheory.Abelian.Exact

/-!
# Exact functors

In this file, it is shown that additive functors which preserves homology
also preserves finite limits and finite colimits.

## Main results

Let `F : C ‚•§ D` be an additive functor:

- `Functor.preservesFiniteLimitsOfPreservesHomology`: if `F` preserves homology, then `F` preserves
  finite limits.
- `Functor.preservesFiniteColimitsOfPreservesHomology`: if `F` preserves homology, then `F`
  preserves finite colimits.

If we further assume that `C` and `D` are abelian categories, then we have:

- `Functor.preservesFiniteLimits_tfae`: the following are equivalent:
  1. for every short exact sequence `0 ‚ü∂ A ‚ü∂ B ‚ü∂ C ‚ü∂ 0`,
    `0 ‚ü∂ F(A) ‚ü∂ F(B) ‚ü∂ F(C) ‚ü∂ 0` is exact.
  2. for every exact sequence `A ‚ü∂ B ‚ü∂ C` where `A ‚ü∂ B` is mono,
    `F(A) ‚ü∂ F(B) ‚ü∂ F(C)` is exact and `F(A) ‚ü∂ F(B)` is mono.
  3. `F` preserves kernels.
  4. `F` preserves finite limits.

- `Functor.preservesFiniteColimits_tfae`: the following are equivalent:
  1. for every short exact sequence `0 ‚ü∂ A ‚ü∂ B ‚ü∂ C ‚ü∂ 0`,
    `F(A) ‚ü∂ F(B) ‚ü∂ F(C) ‚ü∂ 0` is exact.
  2. for every exact sequence `A ‚ü∂ B ‚ü∂ C` where `B ‚ü∂ C` is epi,
    `F(A) ‚ü∂ F(B) ‚ü∂ F(C)` is exact and `F(B) ‚ü∂ F(C)` is epi.
  3. `F` preserves cokernels.
  4. `F` preserves finite colimits.

- `Functor.exact_tfae`: the following are equivalent:
  1. for every short exact sequence `0 ‚ü∂ A ‚ü∂ B ‚ü∂ C ‚ü∂ 0`,
    `0 ‚ü∂ F(A) ‚ü∂ F(B) ‚ü∂ F(C) ‚ü∂ 0` is exact.
  2. for every exact sequence `A ‚ü∂ B ‚ü∂ C`, `F(A) ‚ü∂ F(B) ‚ü∂ F(C)` is exact.
  3. `F` preserves homology.
  4. `F` preserves both finite limits and finite colimits.

-/

namespace CategoryTheory

open Limits ZeroObject ShortComplex

namespace Functor

section

variable {C D : Type*} [Category C] [Category D] [Preadditive C] [Preadditive D]
  (F : C ‚•§ D) [F.Additive] [F.PreservesHomology] [HasZeroObject C]

/-- An additive functor which preserves homology preserves finite limits. -/
noncomputable def preservesFiniteLimitsOfPreservesHomology
    [HasFiniteProducts C] [HasKernels C] : PreservesFiniteLimits F := by
  have := fun {X Y : C} (f : X ‚ü∂ Y) ‚Ü¶ PreservesHomology.preservesKernel F f
  have : HasBinaryBiproducts C := HasBinaryBiproducts.of_hasBinaryProducts
  have : HasEqualizers C := Preadditive.hasEqualizers_of_hasKernels
  have : HasZeroObject D :=
    ‚ü®F.obj 0, by rw [IsZero.iff_id_eq_zero, ‚Üê F.map_id, id_zero, F.map_zero]‚ü©
  exact preservesFiniteLimitsOfPreservesKernels F

/-- An additive which preserves homology preserves finite colimits. -/
noncomputable def preservesFiniteColimitsOfPreservesHomology
    [HasFiniteCoproducts C] [HasCokernels C] : PreservesFiniteColimits F := by
  have := fun {X Y : C} (f : X ‚ü∂ Y) ‚Ü¶ PreservesHomology.preservesCokernel F f
  have : HasBinaryBiproducts C := HasBinaryBiproducts.of_hasBinaryCoproducts
  have : HasCoequalizers C := Preadditive.hasCoequalizers_of_hasCokernels
  have : HasZeroObject D :=
    ‚ü®F.obj 0, by rw [IsZero.iff_id_eq_zero, ‚Üê F.map_id, id_zero, F.map_zero]‚ü©
  exact preservesFiniteColimitsOfPreservesCokernels F

end

section

variable {C D : Type*} [Category C] [Category D] [Abelian C] [Abelian D]
variable (F : C ‚•§ D) [F.Additive]

/--
If a functor `F : C ‚•§ D` preserves short exact sequences on the left hand side, (i.e.
if `0 ‚ü∂ A ‚ü∂ B ‚ü∂ C ‚ü∂ 0` is exact then `0 ‚ü∂ F(A) ‚ü∂ F(B) ‚ü∂ F(C)` is exact)
then it preserves monomorphism.
-/
lemma preservesMonomorphisms_of_preserves_shortExact_left
    (h : ‚àÄ (S : ShortComplex C), S.ShortExact ‚Üí (S.map F).Exact ‚àß Mono (F.map S.f)) :
    F.PreservesMonomorphisms where
  preserves f := h _ { exact := exact_cokernel f } |>.2

/--
For an addivite functor `F : C ‚•§ D` between abelian categories, the following are equivalent:
- `F` preserves short exact sequences on the left hand side, i.e. if `0 ‚ü∂ A ‚ü∂ B ‚ü∂ C ‚ü∂ 0` is exact
  then `0 ‚ü∂ F(A) ‚ü∂ F(B) ‚ü∂ F(C)` is exact.
- `F` preserves exact sequences on the left hand side, i.e. if `A ‚ü∂ B ‚ü∂ C` is exact where `A ‚ü∂ B`
  is mono, then `F(A) ‚ü∂ F(B) ‚ü∂ F(C)` is exact and `F(A) ‚ü∂ F(B)` is mono as well.
- `F` preserves kernels.
- `F` preserves finite limits.
-/
lemma preservesFiniteLimits_tfae : List.TFAE
    [
      ‚àÄ (S : ShortComplex C), S.ShortExact ‚Üí (S.map F).Exact ‚àß Mono (F.map S.f),
      ‚àÄ (S : ShortComplex C), S.Exact ‚àß Mono S.f ‚Üí (S.map F).Exact ‚àß Mono (F.map S.f),
      ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), Nonempty <| PreservesLimit (parallelPair f 0) F,
      Nonempty <| PreservesFiniteLimits F
    ] := by
  tfae_have 1 ‚Üí 2
  | hF, S, ‚ü®hS, hf‚ü© => by
    have := preservesMonomorphisms_of_preserves_shortExact_left F hF
    refine ‚ü®?_, inferInstance‚ü©
    let T := ShortComplex.mk S.f (Abelian.coimage.œÄ S.g) (Abelian.comp_coimage_œÄ_eq_zero S.zero)
    let œÜ : T.map F ‚ü∂ S.map F :=
      { œÑ‚ÇÅ := ùüô _
        œÑ‚ÇÇ := ùüô _
        œÑ‚ÇÉ := F.map <| Abelian.factorThruCoimage S.g
        comm‚ÇÇ‚ÇÉ := show ùüô _ ‚â´ F.map _ = F.map (cokernel.œÄ _) ‚â´ _ by
          rw [Category.id_comp, ‚Üê F.map_comp, cokernel.œÄ_desc] }
    exact (exact_iff_of_epi_of_isIso_of_mono œÜ).1 (hF T ‚ü®(S.exact_iff_exact_coimage_œÄ).1 hS‚ü©).1

  tfae_have 2 ‚Üí 3
  | hF, X, Y, f => by
    refine ‚ü®preservesLimitOfPreservesLimitCone (kernelIsKernel f) ?_‚ü©
    apply (KernelFork.isLimitMapConeEquiv _ F).2
    let S := ShortComplex.mk _ _ (kernel.condition f)
    let hS := hF S ‚ü®exact_kernel f, inferInstance‚ü©
    have : Mono (S.map F).f := hS.2
    exact hS.1.fIsKernel

  tfae_have 3 ‚Üí 4
  | hF => by
    have := fun X Y (f : X ‚ü∂ Y) ‚Ü¶ (hF f).some
    exact ‚ü®preservesFiniteLimitsOfPreservesKernels F‚ü©

  tfae_have 4 ‚Üí 1
  | ‚ü®_‚ü©, S, hS =>
    (S.map F).exact_and_mono_f_iff_f_is_kernel |>.2 ‚ü®KernelFork.mapIsLimit _ hS.fIsKernel F‚ü©

  tfae_finish

/--
If a functor `F : C ‚•§ D` preserves exact sequences on the right hand side (i.e.
if `0 ‚ü∂ A ‚ü∂ B ‚ü∂ C ‚ü∂ 0` is exact then `F(A) ‚ü∂ F(B) ‚ü∂ F(C) ‚ü∂ 0` is exact),
then it preserves epimorphisms.
-/
lemma preservesEpimorphisms_of_preserves_shortExact_right
    (h : ‚àÄ (S : ShortComplex C), S.ShortExact ‚Üí (S.map F).Exact ‚àß Epi (F.map S.g)) :
    F.PreservesEpimorphisms where
  preserves f := h _ { exact := exact_kernel f } |>.2

/--
For an addivite functor `F : C ‚•§ D` between abelian categories, the following are equivalent:
- `F` preserves short exact sequences on the right hand side, i.e. if `0 ‚ü∂ A ‚ü∂ B ‚ü∂ C ‚ü∂ 0` is
  exact then `F(A) ‚ü∂ F(B) ‚ü∂ F(C) ‚ü∂ 0` is exact.
- `F` preserves exact sequences on the right hand side, i.e. if `A ‚ü∂ B ‚ü∂ C` is exact where `B ‚ü∂ C`
  is epi, then `F(A) ‚ü∂ F(B) ‚ü∂ F(C) ‚ü∂ 0` is exact and `F(B) ‚ü∂ F(C)` is epi as well.
- `F` preserves cokernels.
- `F` preserves finite colimits.
-/
lemma preservesFiniteColimits_tfae : List.TFAE
    [
      ‚àÄ (S : ShortComplex C), S.ShortExact ‚Üí (S.map F).Exact ‚àß Epi (F.map S.g),
      ‚àÄ (S : ShortComplex C), S.Exact ‚àß Epi S.g ‚Üí (S.map F).Exact ‚àß Epi (F.map S.g),
      ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), Nonempty <| PreservesColimit (parallelPair f 0) F,
      Nonempty <| PreservesFiniteColimits F
    ] := by
  tfae_have 1 ‚Üí 2
  | hF, S, ‚ü®hS, hf‚ü© => by
    have := preservesEpimorphisms_of_preserves_shortExact_right F hF
    refine ‚ü®?_, inferInstance‚ü©
    let T := ShortComplex.mk (Abelian.image.Œπ S.f) S.g (Abelian.image_Œπ_comp_eq_zero S.zero)
    let œÜ : S.map F ‚ü∂ T.map F :=
      { œÑ‚ÇÅ := F.map <| Abelian.factorThruImage S.f
        œÑ‚ÇÇ := ùüô _
        œÑ‚ÇÉ := ùüô _
        comm‚ÇÅ‚ÇÇ := show _ ‚â´ F.map (kernel.Œπ _) = F.map _ ‚â´ ùüô _ by
          rw [‚Üê F.map_comp, Abelian.image.fac, Category.comp_id] }
    exact (exact_iff_of_epi_of_isIso_of_mono œÜ).2 (hF T ‚ü®(S.exact_iff_exact_image_Œπ).1 hS‚ü©).1

  tfae_have 2 ‚Üí 3
  | hF, X, Y, f => by
    refine ‚ü®preservesColimitOfPreservesColimitCocone (cokernelIsCokernel f) ?_‚ü©
    apply (CokernelCofork.isColimitMapCoconeEquiv _ F).2
    let S := ShortComplex.mk _ _ (cokernel.condition f)
    let hS := hF S ‚ü®exact_cokernel f, inferInstance‚ü©
    have : Epi (S.map F).g := hS.2
    exact hS.1.gIsCokernel

  tfae_have 3 ‚Üí 4
  | hF => by
    have := fun X Y (f : X ‚ü∂ Y) ‚Ü¶ (hF f).some
    exact ‚ü®preservesFiniteColimitsOfPreservesCokernels F‚ü©

  tfae_have 4 ‚Üí 1
  | ‚ü®_‚ü©, S, hS => (S.map F).exact_and_epi_g_iff_g_is_cokernel |>.2
    ‚ü®CokernelCofork.mapIsColimit _ hS.gIsCokernel F‚ü©

  tfae_finish

/--
For an additive functor `F : C ‚•§ D` between abelian categories, the following are equivalent:
- `F` preserves short exact sequences, i.e. if `0 ‚ü∂ A ‚ü∂ B ‚ü∂ C ‚ü∂ 0` is exact then
  `0 ‚ü∂ F(A) ‚ü∂ F(B) ‚ü∂ F(C) ‚ü∂ 0` is exact.
- `F` preserves exact sequences, i.e. if `A ‚ü∂ B ‚ü∂ C` is exact then `F(A) ‚ü∂ F(B) ‚ü∂ F(C)` is exact.
- `F` preserves homology.
- `F` preserves both finite limits and finite colimits.
-/
lemma exact_tfae : List.TFAE
    [
      ‚àÄ (S : ShortComplex C), S.ShortExact ‚Üí (S.map F).ShortExact,
      ‚àÄ (S : ShortComplex C), S.Exact ‚Üí (S.map F).Exact,
      Nonempty (PreservesHomology F),
      Nonempty (PreservesFiniteLimits F) ‚àß Nonempty (PreservesFiniteColimits F)
    ] := by
  tfae_have 1 ‚Üí 3
  | hF => by
    refine ‚ü®fun {X Y} f ‚Ü¶ ?_, fun {X Y} f ‚Ü¶ ?_‚ü©
    ¬∑ have h := (preservesFiniteLimits_tfae F |>.out 0 2 |>.1 fun S hS ‚Ü¶
        And.intro (hF S hS).exact (hF S hS).mono_f)
      exact h f |>.some
    ¬∑ have h := (preservesFiniteColimits_tfae F |>.out 0 2 |>.1 fun S hS ‚Ü¶
        And.intro (hF S hS).exact (hF S hS).epi_g)
      exact h f |>.some

  tfae_have 2 ‚Üí 1
  | hF, S, hS => by
    have : Mono (S.map F).f := exact_iff_mono _ (by simp) |>.1 <|
      hF (.mk (0 : 0 ‚ü∂ S.X‚ÇÅ) S.f <| by simp) (exact_iff_mono _ (by simp) |>.2 hS.mono_f)
    have : Epi (S.map F).g := exact_iff_epi _ (by simp) |>.1 <|
      hF (.mk S.g (0 : S.X‚ÇÉ ‚ü∂ 0) <| by simp) (exact_iff_epi _ (by simp) |>.2 hS.epi_g)
    exact ‚ü®hF S hS.exact‚ü©

  tfae_have 3 ‚Üí 4
  | ‚ü®h‚ü© => ‚ü®‚ü®preservesFiniteLimitsOfPreservesHomology F‚ü©,
    ‚ü®preservesFiniteColimitsOfPreservesHomology F‚ü©‚ü©

  tfae_have 4 ‚Üí 2
  | ‚ü®‚ü®h1‚ü©, ‚ü®h2‚ü©‚ü©, _, h => h.map F

  tfae_finish

end

end Functor

end CategoryTheory
