/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou, Jujian Zhang
-/
import Mathlib.Algebra.Homology.ShortComplex.PreservesHomology
import Mathlib.Algebra.Homology.ShortComplex.ShortExact
import Mathlib.Algebra.Homology.ShortComplex.Abelian
import Mathlib.CategoryTheory.Preadditive.LeftExact
import Mathlib.CategoryTheory.Abelian.Exact

/-!
# Exact functors

In this file, it is shown that additive functors which preserves homology
also preserves finite limits and finite colimits.

## Main results

Let `F : C â¥¤ D` be an additive functor:

- `Functor.preservesFiniteLimits_of_preservesHomology`: if `F` preserves homology,
  then `F` preserves finite limits.
- `Functor.preservesFiniteColimits_of_preservesHomology`: if `F` preserves homology, then `F`
  preserves finite colimits.

If we further assume that `C` and `D` are abelian categories, then we have:

- `Functor.preservesFiniteLimits_tfae`: the following are equivalent:
  1. for every short exact sequence `0 âŸ¶ A âŸ¶ B âŸ¶ C âŸ¶ 0`,
    `0 âŸ¶ F(A) âŸ¶ F(B) âŸ¶ F(C) âŸ¶ 0` is exact.
  2. for every exact sequence `A âŸ¶ B âŸ¶ C` where `A âŸ¶ B` is mono,
    `F(A) âŸ¶ F(B) âŸ¶ F(C)` is exact and `F(A) âŸ¶ F(B)` is mono.
  3. `F` preserves kernels.
  4. `F` preserves finite limits.

- `Functor.preservesFiniteColimits_tfae`: the following are equivalent:
  1. for every short exact sequence `0 âŸ¶ A âŸ¶ B âŸ¶ C âŸ¶ 0`,
    `F(A) âŸ¶ F(B) âŸ¶ F(C) âŸ¶ 0` is exact.
  2. for every exact sequence `A âŸ¶ B âŸ¶ C` where `B âŸ¶ C` is epi,
    `F(A) âŸ¶ F(B) âŸ¶ F(C)` is exact and `F(B) âŸ¶ F(C)` is epi.
  3. `F` preserves cokernels.
  4. `F` preserves finite colimits.

- `Functor.exact_tfae`: the following are equivalent:
  1. for every short exact sequence `0 âŸ¶ A âŸ¶ B âŸ¶ C âŸ¶ 0`,
    `0 âŸ¶ F(A) âŸ¶ F(B) âŸ¶ F(C) âŸ¶ 0` is exact.
  2. for every exact sequence `A âŸ¶ B âŸ¶ C`, `F(A) âŸ¶ F(B) âŸ¶ F(C)` is exact.
  3. `F` preserves homology.
  4. `F` preserves both finite limits and finite colimits.

-/

namespace CategoryTheory

open Limits ZeroObject ShortComplex

namespace Functor

section

variable {C D : Type*} [Category C] [Category D] [Preadditive C] [Preadditive D]
  (F : C â¥¤ D) [F.Additive] [F.PreservesHomology] [HasZeroObject C]

/-- An additive functor which preserves homology preserves finite limits. -/
lemma preservesFiniteLimits_of_preservesHomology
    [HasFiniteProducts C] [HasKernels C] : PreservesFiniteLimits F := by
  have := fun {X Y : C} (f : X âŸ¶ Y) â†¦ PreservesHomology.preservesKernel F f
  have : HasBinaryBiproducts C := HasBinaryBiproducts.of_hasBinaryProducts
  have : HasEqualizers C := Preadditive.hasEqualizers_of_hasKernels
  have : HasZeroObject D :=
    âŸ¨F.obj 0, by rw [IsZero.iff_id_eq_zero, â† F.map_id, id_zero, F.map_zero]âŸ©
  exact preservesFiniteLimits_of_preservesKernels F

/-- An additive which preserves homology preserves finite colimits. -/
lemma preservesFiniteColimits_of_preservesHomology
    [HasFiniteCoproducts C] [HasCokernels C] : PreservesFiniteColimits F := by
  have := fun {X Y : C} (f : X âŸ¶ Y) â†¦ PreservesHomology.preservesCokernel F f
  have : HasBinaryBiproducts C := HasBinaryBiproducts.of_hasBinaryCoproducts
  have : HasCoequalizers C := Preadditive.hasCoequalizers_of_hasCokernels
  have : HasZeroObject D :=
    âŸ¨F.obj 0, by rw [IsZero.iff_id_eq_zero, â† F.map_id, id_zero, F.map_zero]âŸ©
  exact preservesFiniteColimits_of_preservesCokernels F

end

section

variable {C D : Type*} [Category C] [Category D] [Abelian C] [Abelian D]
variable (F : C â¥¤ D) [F.Additive]

/--
If a functor `F : C â¥¤ D` preserves short exact sequences on the left-hand side, (i.e.
if `0 âŸ¶ A âŸ¶ B âŸ¶ C âŸ¶ 0` is exact then `0 âŸ¶ F(A) âŸ¶ F(B) âŸ¶ F(C)` is exact)
then it preserves monomorphism.
-/
lemma preservesMonomorphisms_of_preserves_shortExact_left
    (h : âˆ€ (S : ShortComplex C), S.ShortExact â†’ (S.map F).Exact âˆ§ Mono (F.map S.f)) :
    F.PreservesMonomorphisms where
  preserves f := h _ { exact := exact_cokernel f } |>.2

/--
For an additive functor `F : C â¥¤ D` between abelian categories, the following are equivalent:
- `F` preserves short exact sequences on the left-hand side, i.e. if `0 âŸ¶ A âŸ¶ B âŸ¶ C âŸ¶ 0` is exact
  then `0 âŸ¶ F(A) âŸ¶ F(B) âŸ¶ F(C)` is exact.
- `F` preserves exact sequences on the left-hand side, i.e. if `A âŸ¶ B âŸ¶ C` is exact where `A âŸ¶ B`
  is mono, then `F(A) âŸ¶ F(B) âŸ¶ F(C)` is exact and `F(A) âŸ¶ F(B)` is mono as well.
- `F` preserves kernels.
- `F` preserves finite limits.
-/
lemma preservesFiniteLimits_tfae : List.TFAE
    [
      âˆ€ (S : ShortComplex C), S.ShortExact â†’ (S.map F).Exact âˆ§ Mono (F.map S.f),
      âˆ€ (S : ShortComplex C), S.Exact âˆ§ Mono S.f â†’ (S.map F).Exact âˆ§ Mono (F.map S.f),
      âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), PreservesLimit (parallelPair f 0) F,
      PreservesFiniteLimits F
    ] := by
  tfae_have 1 â†’ 2
  | hF, S, âŸ¨hS, hfâŸ© => by
    have := preservesMonomorphisms_of_preserves_shortExact_left F hF
    refine âŸ¨?_, inferInstanceâŸ©
    let T := ShortComplex.mk S.f (Abelian.coimage.Ï€ S.g) (Abelian.comp_coimage_Ï€_eq_zero S.zero)
    let Ï† : T.map F âŸ¶ S.map F :=
      { Ï„â‚ := ğŸ™ _
        Ï„â‚‚ := ğŸ™ _
        Ï„â‚ƒ := F.map <| Abelian.factorThruCoimage S.g
        commâ‚‚â‚ƒ := show ğŸ™ _ â‰« F.map _ = F.map (cokernel.Ï€ _) â‰« _ by
          rw [Category.id_comp, â† F.map_comp, cokernel.Ï€_desc] }
    exact (exact_iff_of_epi_of_isIso_of_mono Ï†).1 (hF T âŸ¨(S.exact_iff_exact_coimage_Ï€).1 hSâŸ©).1
  tfae_have 2 â†’ 3
  | hF, X, Y, f => by
    refine preservesLimit_of_preserves_limit_cone (kernelIsKernel f) ?_
    apply (KernelFork.isLimitMapConeEquiv _ F).2
    let S := ShortComplex.mk _ _ (kernel.condition f)
    let hS := hF S âŸ¨exact_kernel f, inferInstanceâŸ©
    have : Mono (S.map F).f := hS.2
    exact hS.1.fIsKernel
  tfae_have 3 â†’ 4
  | hF => by
    exact preservesFiniteLimits_of_preservesKernels F
  tfae_have 4 â†’ 1
  | âŸ¨_âŸ©, S, hS =>
    (S.map F).exact_and_mono_f_iff_f_is_kernel |>.2 âŸ¨KernelFork.mapIsLimit _ hS.fIsKernel FâŸ©
  tfae_finish

/--
If a functor `F : C â¥¤ D` preserves exact sequences on the right-hand side (i.e.
if `0 âŸ¶ A âŸ¶ B âŸ¶ C âŸ¶ 0` is exact then `F(A) âŸ¶ F(B) âŸ¶ F(C) âŸ¶ 0` is exact),
then it preserves epimorphisms.
-/
lemma preservesEpimorphisms_of_preserves_shortExact_right
    (h : âˆ€ (S : ShortComplex C), S.ShortExact â†’ (S.map F).Exact âˆ§ Epi (F.map S.g)) :
    F.PreservesEpimorphisms where
  preserves f := h _ { exact := exact_kernel f } |>.2

/--
For an additive functor `F : C â¥¤ D` between abelian categories, the following are equivalent:
- `F` preserves short exact sequences on the right-hand side, i.e. if `0 âŸ¶ A âŸ¶ B âŸ¶ C âŸ¶ 0` is
  exact then `F(A) âŸ¶ F(B) âŸ¶ F(C) âŸ¶ 0` is exact.
- `F` preserves exact sequences on the right-hand side, i.e. if `A âŸ¶ B âŸ¶ C` is exact where `B âŸ¶ C`
  is epi, then `F(A) âŸ¶ F(B) âŸ¶ F(C) âŸ¶ 0` is exact and `F(B) âŸ¶ F(C)` is epi as well.
- `F` preserves cokernels.
- `F` preserves finite colimits.
-/
lemma preservesFiniteColimits_tfae : List.TFAE
    [
      âˆ€ (S : ShortComplex C), S.ShortExact â†’ (S.map F).Exact âˆ§ Epi (F.map S.g),
      âˆ€ (S : ShortComplex C), S.Exact âˆ§ Epi S.g â†’ (S.map F).Exact âˆ§ Epi (F.map S.g),
      âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), PreservesColimit (parallelPair f 0) F,
      PreservesFiniteColimits F
    ] := by
  tfae_have 1 â†’ 2
  | hF, S, âŸ¨hS, hfâŸ© => by
    have := preservesEpimorphisms_of_preserves_shortExact_right F hF
    refine âŸ¨?_, inferInstanceâŸ©
    let T := ShortComplex.mk (Abelian.image.Î¹ S.f) S.g (Abelian.image_Î¹_comp_eq_zero S.zero)
    let Ï† : S.map F âŸ¶ T.map F :=
      { Ï„â‚ := F.map <| Abelian.factorThruImage S.f
        Ï„â‚‚ := ğŸ™ _
        Ï„â‚ƒ := ğŸ™ _
        commâ‚â‚‚ := show _ â‰« F.map (kernel.Î¹ _) = F.map _ â‰« ğŸ™ _ by
          rw [â† F.map_comp, Abelian.image.fac, Category.comp_id] }
    exact (exact_iff_of_epi_of_isIso_of_mono Ï†).2 (hF T âŸ¨(S.exact_iff_exact_image_Î¹).1 hSâŸ©).1
  tfae_have 2 â†’ 3
  | hF, X, Y, f => by
    refine preservesColimit_of_preserves_colimit_cocone (cokernelIsCokernel f) ?_
    apply (CokernelCofork.isColimitMapCoconeEquiv _ F).2
    let S := ShortComplex.mk _ _ (cokernel.condition f)
    let hS := hF S âŸ¨exact_cokernel f, inferInstanceâŸ©
    have : Epi (S.map F).g := hS.2
    exact hS.1.gIsCokernel
  tfae_have 3 â†’ 4
  | hF => by
    exact preservesFiniteColimits_of_preservesCokernels F
  tfae_have 4 â†’ 1
  | âŸ¨_âŸ©, S, hS => (S.map F).exact_and_epi_g_iff_g_is_cokernel |>.2
    âŸ¨CokernelCofork.mapIsColimit _ hS.gIsCokernel FâŸ©
  tfae_finish

/--
For an additive functor `F : C â¥¤ D` between abelian categories, the following are equivalent:
- `F` preserves short exact sequences, i.e. if `0 âŸ¶ A âŸ¶ B âŸ¶ C âŸ¶ 0` is exact then
  `0 âŸ¶ F(A) âŸ¶ F(B) âŸ¶ F(C) âŸ¶ 0` is exact.
- `F` preserves exact sequences, i.e. if `A âŸ¶ B âŸ¶ C` is exact then `F(A) âŸ¶ F(B) âŸ¶ F(C)` is exact.
- `F` preserves homology.
- `F` preserves both finite limits and finite colimits.
-/
lemma exact_tfae : List.TFAE
    [
      âˆ€ (S : ShortComplex C), S.ShortExact â†’ (S.map F).ShortExact,
      âˆ€ (S : ShortComplex C), S.Exact â†’ (S.map F).Exact,
      PreservesHomology F,
      PreservesFiniteLimits F âˆ§ PreservesFiniteColimits F
    ] := by
  tfae_have 1 â†’ 3
  | hF => by
    refine âŸ¨fun {X Y} f â†¦ ?_, fun {X Y} f â†¦ ?_âŸ©
    Â· have h := (preservesFiniteLimits_tfae F |>.out 0 2 |>.1 fun S hS â†¦
        And.intro (hF S hS).exact (hF S hS).mono_f)
      exact h f
    Â· have h := (preservesFiniteColimits_tfae F |>.out 0 2 |>.1 fun S hS â†¦
        And.intro (hF S hS).exact (hF S hS).epi_g)
      exact h f
  tfae_have 2 â†’ 1
  | hF, S, hS => by
    have : Mono (S.map F).f := exact_iff_mono _ (by simp) |>.1 <|
      hF (.mk (0 : 0 âŸ¶ S.Xâ‚) S.f <| by simp) (exact_iff_mono _ (by simp) |>.2 hS.mono_f)
    have : Epi (S.map F).g := exact_iff_epi _ (by simp) |>.1 <|
      hF (.mk S.g (0 : S.Xâ‚ƒ âŸ¶ 0) <| by simp) (exact_iff_epi _ (by simp) |>.2 hS.epi_g)
    exact âŸ¨hF S hS.exactâŸ©
  tfae_have 3 â†’ 4
  | h => âŸ¨preservesFiniteLimits_of_preservesHomology F,
      preservesFiniteColimits_of_preservesHomology FâŸ©
  tfae_have 4 â†’ 2
  | âŸ¨h1, h2âŸ©, _, h => h.map F
  tfae_finish

end

end Functor

end CategoryTheory
