/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou, Jujian Zhang
-/
import Mathlib.Algebra.Homology.ShortComplex.PreservesHomology
import Mathlib.Algebra.Homology.ShortComplex.ShortExact
import Mathlib.Algebra.Homology.ShortComplex.Abelian
import Mathlib.CategoryTheory.Preadditive.LeftExact
import Mathlib.CategoryTheory.Abelian.Exact

/-!
# Exact functors

In this file, it is shown that additive functors which preserves homology
also preserves finite limits and finite colimits.

## Main results

Let `F : C â¥¤ D` be an additive functor:

- `Functor.preservesFiniteLimitsOfPreservesHomology`: if `F` preserves homology, then `F` preserves
  finite limits.
- `Functor.preservesFiniteColimitsOfPreservesHomology`: if `F` preserves homology, then `F`
  preserves finite colimits.

If we further assume that `C` and `D` are abelian categories, then we have:

- `Functor.preservesFiniteLimits_tfae`: the following are equivalent:
  1. for every short exact sequence `0 âŸ¶ A âŸ¶ B âŸ¶ C âŸ¶ 0`,
    `0 âŸ¶ F(A) âŸ¶ F(B) âŸ¶ F(C) âŸ¶ 0` is exact.
  2. for every exact sequence `A âŸ¶ B âŸ¶ C` where `A âŸ¶ B` is mono,
    `F(A) âŸ¶ F(B) âŸ¶ F(C)` is exact and `F(A) âŸ¶ F(B)` is mono.
  3. `F` preserves kernels.
  4. `F` preserves finite limits.

- `Functor.preservesFiniteColimits_tfae`: the following are equivalent:
  1. for every short exact sequence `0 âŸ¶ A âŸ¶ B âŸ¶ C âŸ¶ 0`,
    `F(A) âŸ¶ F(B) âŸ¶ F(C) âŸ¶ 0` is exact.
  2. for every exact sequence `A âŸ¶ B âŸ¶ C` where `B âŸ¶ C` is epi,
    `F(A) âŸ¶ F(B) âŸ¶ F(C)` is exact and `F(B) âŸ¶ F(C)` is epi.
  3. `F` preserves cokernels.
  4. `F` preserves finite colimits.

- `Functor.exact_tfae`: the following are equivalent:
  1. for every short exact sequence `0 âŸ¶ A âŸ¶ B âŸ¶ C âŸ¶ 0`,
    `0 âŸ¶ F(A) âŸ¶ F(B) âŸ¶ F(C) âŸ¶ 0` is exact.
  2. for every exact sequence `A âŸ¶ B âŸ¶ C`, `F(A) âŸ¶ F(B) âŸ¶ F(C)` is exact.
  3. `F` preserves both finite limits and finite colimits.
  4. `F` preserves homology.

-/

namespace CategoryTheory

open Limits ZeroObject

namespace Functor

section

variable {C D : Type*} [Category C] [Category D] [Preadditive C] [Preadditive D]
  (F : C â¥¤ D) [F.Additive] [F.PreservesHomology] [HasZeroObject C]

/-- An additive functor which preserves homology preserves finite limits. -/
noncomputable def preservesFiniteLimitsOfPreservesHomology
    [HasFiniteProducts C] [HasKernels C] : PreservesFiniteLimits F := by
  have := fun {X Y : C} (f : X âŸ¶ Y) => PreservesHomology.preservesKernel F f
  have : HasBinaryBiproducts C := HasBinaryBiproducts.of_hasBinaryProducts
  have : HasEqualizers C := Preadditive.hasEqualizers_of_hasKernels
  have : HasZeroObject D :=
    âŸ¨F.obj 0, by rw [IsZero.iff_id_eq_zero, â† F.map_id, id_zero, F.map_zero]âŸ©
  exact preservesFiniteLimitsOfPreservesKernels F

/-- An additive which preserves homology preserves finite colimits. -/
noncomputable def preservesFiniteColimitsOfPreservesHomology
    [HasFiniteCoproducts C] [HasCokernels C] : PreservesFiniteColimits F := by
  have := fun {X Y : C} (f : X âŸ¶ Y) => PreservesHomology.preservesCokernel F f
  have : HasBinaryBiproducts C := HasBinaryBiproducts.of_hasBinaryCoproducts
  have : HasCoequalizers C := Preadditive.hasCoequalizers_of_hasCokernels
  have : HasZeroObject D :=
    âŸ¨F.obj 0, by rw [IsZero.iff_id_eq_zero, â† F.map_id, id_zero, F.map_zero]âŸ©
  exact preservesFiniteColimitsOfPreservesCokernels F

end

section

variable {C D : Type*} [Category C] [Category D] [Abelian C] [Abelian D]
variable (F : C â¥¤ D) [F.Additive]

/--
If a functor `F : C â¥¤ D` preserves short exact sequences on the left hand side, (i.e.
if `0 âŸ¶ A âŸ¶ B âŸ¶ C âŸ¶ 0` is exact then `0 âŸ¶ F(A) âŸ¶ F(B) âŸ¶ F(C)` is exact)
then it preserves
monomorphism.
-/
lemma preservesMonomorphisms_of_preserves_shortExact_left
    (h : âˆ€ (S : ShortComplex C), S.ShortExact â†’ (S.map F).Exact âˆ§ Mono (F.map S.f)) :
    F.PreservesMonomorphisms where
  preserves f := h _ { exact := ShortComplex.exact_cokernel f } |>.2

/--
For an addivite functor `F : C â¥¤ D` between abelian categories, the following are equivalent:
- `F` preserves short exact sequences on the left hand side, i.e. if `0 âŸ¶ A âŸ¶ B âŸ¶ C âŸ¶ 0` is exact
  then `0 âŸ¶ F(A) âŸ¶ F(B) âŸ¶ F(C)` is exact.
- `F` preserves exact sequences on the left hand side, i.e. if `A âŸ¶ B âŸ¶ C` is exact where `A âŸ¶ B`
  is mono, then `F(A) âŸ¶ F(B) âŸ¶ F(C)` is exact and `F(A) âŸ¶ F(B)` is mono as well.
- `F` preserves kernels.
- `F` preserves finite limits.
-/
lemma preservesFiniteLimits_tfae : List.TFAE
    [
      âˆ€ (S : ShortComplex C), S.ShortExact â†’ (S.map F).Exact âˆ§ Mono (F.map S.f),
      âˆ€ (S : ShortComplex C), S.Exact âˆ§ Mono S.f â†’ (S.map F).Exact âˆ§ Mono (F.map S.f),
      âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), Nonempty $ PreservesLimit (parallelPair f 0) F,
      Nonempty $ PreservesFiniteLimits F
    ] := by
  tfae_have 1 â†’ 2
  Â· rintro hF S âŸ¨hS, hfâŸ©
    have := preservesMonomorphisms_of_preserves_shortExact_left F hF
    refine âŸ¨?_, inferInstanceâŸ©
    let T := ShortComplex.mk S.f (Abelian.coimage.Ï€ S.g) (Abelian.comp_coimage_Ï€_eq_zero S.zero)
    let Ï† : T.map F âŸ¶ S.map F :=
      { Ï„â‚ := ðŸ™ _
        Ï„â‚‚ := ðŸ™ _
        Ï„â‚ƒ := F.map (Abelian.factorThruCoimage S.g)
        commâ‚‚â‚ƒ := by
          dsimp
          rw [Category.id_comp, â† F.map_comp, cokernel.Ï€_desc] }
    exact (ShortComplex.exact_iff_of_epi_of_isIso_of_mono Ï†).1
      (hF T { exact := (S.exact_iff_exact_coimage_Ï€).1 hS }).1

  tfae_have 2 â†’ 3
  Â· intro hF X Y f
    refine âŸ¨preservesLimitOfPreservesLimitCone (kernelIsKernel f) ?_âŸ©
    apply (KernelFork.isLimitMapConeEquiv _ F).2
    let S := ShortComplex.mk _ _ (kernel.condition f)
    let hS := hF S âŸ¨ShortComplex.exact_kernel f, by infer_instanceâŸ©
    have : Mono (S.map F).f := hS.2
    exact hS.1.fIsKernel

  tfae_have 3 â†’ 4
  Â· intro hF
    have := fun X Y (f : X âŸ¶ Y) â†¦ (hF f).some
    exact âŸ¨by apply preservesFiniteLimitsOfPreservesKernelsâŸ©

  tfae_have 4 â†’ 1
  Â· rintro âŸ¨_âŸ© S hS
    exact (S.map F).exact_and_mono_f_iff_f_is_kernel |>.2
      âŸ¨KernelFork.mapIsLimit _ hS.fIsKernel FâŸ©

  tfae_finish

/--
If a functor `F : C â¥¤ D` preserves exact sequences on the right hand side (i.e.
if `0 âŸ¶ A âŸ¶ B âŸ¶ C âŸ¶ 0` is exact then `F(A) âŸ¶ F(B) âŸ¶ F(C) âŸ¶ 0` is exact),
then it preserves epimorphisms.
-/
lemma preservesEpimorphism_of_preserves_shortExact_right
    (h : âˆ€ (S : ShortComplex C), S.ShortExact â†’ (S.map F).Exact âˆ§ Epi (F.map S.g)) :
    F.PreservesEpimorphisms where
  preserves f := h _ { exact := ShortComplex.exact_kernel f } |>.2

/--
For an addivite functor `F : C â¥¤ D` between abelian categories, the following are equivalent:
- `F` preserves short exact sequences on the right hand side, i.e. if `0 âŸ¶ A âŸ¶ B âŸ¶ C âŸ¶ 0` is
  exact then `F(A) âŸ¶ F(B) âŸ¶ F(C) âŸ¶ 0` is exact.
- `F` preserves exact sequences on the right hand side, i.e. if `A âŸ¶ B âŸ¶ C` is exact where `B âŸ¶ C`
  is epi, then `F(A) âŸ¶ F(B) âŸ¶ F(C) âŸ¶ 0` is exact and `F(B) âŸ¶ F(C)` is epi as well.
- `F` preserves cokernels.
- `F` preserves finite colimits.
-/
lemma preservesFiniteColimits_tfae : List.TFAE
    [
      âˆ€ (S : ShortComplex C), S.ShortExact â†’ (S.map F).Exact âˆ§ Epi (F.map S.g),
      âˆ€ (S : ShortComplex C), S.Exact âˆ§ Epi S.g â†’ (S.map F).Exact âˆ§ Epi (F.map S.g),
      âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), Nonempty $ PreservesColimit (parallelPair f 0) F,
      Nonempty $ PreservesFiniteColimits F
    ] := by
  tfae_have 1 â†’ 2
  Â· rintro hF S âŸ¨hS, hfâŸ©
    have := preservesEpimorphism_of_preserves_shortExact_right F hF
    refine âŸ¨?_, inferInstanceâŸ©
    let T := ShortComplex.mk (Abelian.image.Î¹ S.f) S.g (Abelian.image_Î¹_comp_eq_zero S.zero)
    let Ï† : S.map F âŸ¶ T.map F :=
      { Ï„â‚ := F.map $ (Abelian.factorThruImage S.f)
        Ï„â‚‚ := ðŸ™ _
        Ï„â‚ƒ := ðŸ™ _
        commâ‚â‚‚ := by
          dsimp
          rw [â† F.map_comp, Abelian.image.fac, Category.comp_id] }
    exact (ShortComplex.exact_iff_of_epi_of_isIso_of_mono Ï†).2
      (hF T { exact := (S.exact_iff_exact_image_Î¹).1 hS }).1

  tfae_have 2 â†’ 3
  Â· intro hF X Y f
    refine âŸ¨preservesColimitOfPreservesColimitCocone (cokernelIsCokernel f) ?_âŸ©
    apply (CokernelCofork.isColimitMapCoconeEquiv _ F).2
    let S := ShortComplex.mk _ _ (cokernel.condition f)
    let hS := hF S âŸ¨ShortComplex.exact_cokernel f, by infer_instanceâŸ©
    have : Epi (S.map F).g := hS.2
    exact hS.1.gIsCokernel

  tfae_have 3 â†’ 4
  Â· intro hF
    have := fun X Y (f : X âŸ¶ Y) â†¦ (hF f).some
    exact âŸ¨by apply preservesFiniteColimitsOfPreservesCokernelsâŸ©

  tfae_have 4 â†’ 1
  Â· rintro âŸ¨_âŸ© S hS
    exact (S.map F).exact_and_epi_g_iff_g_is_cokernel |>.2
      âŸ¨CokernelCofork.mapIsColimit _ hS.gIsCokernel FâŸ©

  tfae_finish

open ZeroObject in
/--
For an additive functor `F : C â¥¤ D` between abelian categories, the following are equivalent:
- `F` preserves short exact sequences, i.e. if `0 âŸ¶ A âŸ¶ B âŸ¶ C âŸ¶ 0` is exact then
  `0 âŸ¶ F(A) âŸ¶ F(B) âŸ¶ F(C) âŸ¶ 0` is exact.
- `F` preserves exact sequences, i.e. if `A âŸ¶ B âŸ¶ C` is exact then `F(A) âŸ¶ F(B) âŸ¶ F(C)` is exact.
- `F` preserves both finite limits and finite colimits.
- `F` preserves homology.
-/
lemma exact_tfae : List.TFAE
    [
      âˆ€ (S : ShortComplex C), S.ShortExact â†’ (S.map F).ShortExact,
      âˆ€ (S : ShortComplex C), S.Exact â†’ (S.map F).Exact,
      Nonempty (PreservesFiniteLimits F) âˆ§ Nonempty (PreservesFiniteColimits F),
      Nonempty (PreservesHomology F)
    ] := by
  tfae_have 1 â†’ 4
  Â· intro hF
    refine âŸ¨fun {X Y} f => ?_, fun {X Y} f => ?_âŸ©
    Â· have h := (preservesFiniteLimits_tfae F |>.out 0 2 |>.1 fun S hS =>
        And.intro (hF S hS).exact (hF S hS).mono_f)
      exact h f |>.some
    Â· have h := (preservesFiniteColimits_tfae F |>.out 0 2 |>.1 fun S hS =>
        And.intro (hF S hS).exact (hF S hS).epi_g)
      exact h f |>.some

  tfae_have 2 â†’ 1
  Â· intro hF S hS
    have : Mono (S.map F).f := ShortComplex.exact_iff_mono _ (by simp) |>.1 $
        hF (.mk (0 : 0 âŸ¶ S.Xâ‚) S.f $ by simp)
          (ShortComplex.exact_iff_mono _ (by simp) |>.2 hS.mono_f)
    have : Epi (S.map F).g := ShortComplex.exact_iff_epi _ (by simp) |>.1 $
        hF (.mk S.g (0 : S.Xâ‚ƒ âŸ¶ 0) $ by simp)
          (ShortComplex.exact_iff_epi _ (by simp) |>.2 hS.epi_g)
    exact âŸ¨hF S hS.exactâŸ©

  tfae_have 2 â†’ 3
  Â· intro hF
    refine âŸ¨preservesFiniteLimits_tfae F |>.out 1 3 |>.1 ?_,
      preservesFiniteColimits_tfae F |>.out 1 3 |>.1 ?_âŸ©
    Â· intro S âŸ¨hS1, hS2âŸ©
      refine âŸ¨hF _ hS1, ?_âŸ©
      let s : ShortComplex C := .mk (0 : 0 âŸ¶ S.Xâ‚) S.f $ by simp
      exact (s.map F).exact_iff_mono (by simp) |>.1 $ hF s (s.exact_iff_mono rfl |>.2 hS2)
    Â· intro S âŸ¨hS1, hS2âŸ©
      refine âŸ¨hF _ hS1, ?_âŸ©
      let s : ShortComplex C := .mk S.g (0 : S.Xâ‚ƒ âŸ¶ 0) $ by simp
      exact (s.map F).exact_iff_epi (by simp) |>.1 $ hF s (s.exact_iff_epi rfl |>.2 hS2)

  tfae_have 3 â†’ 4
  Â· rintro âŸ¨âŸ¨_âŸ©, âŸ¨_âŸ©âŸ©
    exact âŸ¨inferInstanceâŸ©
  tfae_have 4 â†’ 3
  Â· rintro âŸ¨hâŸ©
    exact âŸ¨âŸ¨preservesFiniteLimitsOfPreservesHomology FâŸ©,
      âŸ¨preservesFiniteColimitsOfPreservesHomology FâŸ©âŸ©

  tfae_have 3 â†’ 2
  Â· rintro âŸ¨âŸ¨h1âŸ©, âŸ¨h2âŸ©âŸ©
    haveI : PreservesHomology F := inferInstance
    exact fun _ h => h.map F

  tfae_finish

end

end Functor

end CategoryTheory
