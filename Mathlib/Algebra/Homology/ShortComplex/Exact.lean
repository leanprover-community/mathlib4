/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.ShortComplex.PreservesHomology
import Mathlib.Algebra.Homology.ShortComplex.Abelian
import Mathlib.Algebra.Homology.ShortComplex.QuasiIso
import Mathlib.CategoryTheory.Abelian.Exact
import Mathlib.CategoryTheory.MorphismProperty

/-!
# Exact short complexes

When `S : ShortComplex C`, this file defines a structure
`S.Exact` which expresses the exactness of `S`, i.e. there
exists a homology data `h : S.HomologyData` such that
`h.left.H` is zero. When `[S.HasHomology]`, it is equivalent
to the assertion `IsZero S.homology`.

Almost by construction, this notion of exactness is self dual,
see `Exact.op` and `Exact.unop`.

-/

namespace CategoryTheory

open Category Limits ZeroObject Preadditive

variable {C D : Type*} [Category C] [Category D]

namespace ShortComplex

section

variable
  [HasZeroMorphisms C] [HasZeroMorphisms D] (S : ShortComplex C) {Sâ‚ Sâ‚‚ : ShortComplex C}

/-- The assertion that the short complex `S : ShortComplex C` is exact. -/
structure Exact : Prop where
  /-- the condition that there exists an homology data whose `left.H` field is zero -/
  condition : âˆƒ (h : S.HomologyData), IsZero h.left.H

variable {S}

lemma Exact.hasHomology (h : S.Exact) : S.HasHomology :=
  HasHomology.mk' h.condition.choose

lemma Exact.hasZeroObject (h : S.Exact) : HasZeroObject C :=
  âŸ¨h.condition.choose.left.H, h.condition.choose_specâŸ©

variable (S)

lemma exact_iff_isZero_homology [S.HasHomology] :
    S.Exact â†” IsZero S.homology := by
  constructor
  Â· rintro âŸ¨âŸ¨h', zâŸ©âŸ©
    exact IsZero.of_iso z h'.left.homologyIso
  Â· intro h
    exact âŸ¨âŸ¨_, hâŸ©âŸ©

variable {S}

lemma LeftHomologyData.exact_iff [S.HasHomology]
    (h : S.LeftHomologyData) :
    S.Exact â†” IsZero h.H := by
  rw [S.exact_iff_isZero_homology]
  exact Iso.isZero_iff h.homologyIso

lemma RightHomologyData.exact_iff [S.HasHomology]
    (h : S.RightHomologyData) :
    S.Exact â†” IsZero h.H := by
  rw [S.exact_iff_isZero_homology]
  exact Iso.isZero_iff h.homologyIso

variable (S)

lemma exact_iff_isZero_leftHomology [S.HasHomology] :
    S.Exact â†” IsZero S.leftHomology :=
  LeftHomologyData.exact_iff _

lemma exact_iff_isZero_rightHomology [S.HasHomology] :
    S.Exact â†” IsZero S.rightHomology :=
  RightHomologyData.exact_iff _

variable {S}

lemma HomologyData.exact_iff (h : S.HomologyData) :
    S.Exact â†” IsZero h.left.H := by
  haveI := HasHomology.mk' h
  exact LeftHomologyData.exact_iff h.left

lemma HomologyData.exact_iff' (h : S.HomologyData) :
    S.Exact â†” IsZero h.right.H := by
  haveI := HasHomology.mk' h
  exact RightHomologyData.exact_iff h.right

variable (S)

lemma exact_iff_homology_iso_zero [S.HasHomology] [HasZeroObject C] :
    S.Exact â†” Nonempty (S.homology â‰… 0) := by
  rw [exact_iff_isZero_homology]
  constructor
  Â· intro h
    exact âŸ¨h.isoZeroâŸ©
  Â· rintro âŸ¨eâŸ©
    exact IsZero.of_iso (isZero_zero C) e

lemma exact_of_iso (e : Sâ‚ â‰… Sâ‚‚) (h : Sâ‚.Exact) : Sâ‚‚.Exact := by
  obtain âŸ¨âŸ¨h, zâŸ©âŸ© := h
  exact âŸ¨âŸ¨HomologyData.ofIso e h, zâŸ©âŸ©

lemma exact_iff_of_iso (e : Sâ‚ â‰… Sâ‚‚) : Sâ‚.Exact â†” Sâ‚‚.Exact :=
  âŸ¨exact_of_iso e, exact_of_iso e.symmâŸ©

lemma exact_of_isZero_Xâ‚‚ (h : IsZero S.Xâ‚‚) : S.Exact := by
  rw [(HomologyData.ofZeros S (IsZero.eq_of_tgt h _ _) (IsZero.eq_of_src h _ _)).exact_iff]
  exact h

lemma exact_iff_of_epi_of_isIso_of_mono (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [Epi Ï†.Ï„â‚] [IsIso Ï†.Ï„â‚‚] [Mono Ï†.Ï„â‚ƒ] :
    Sâ‚.Exact â†” Sâ‚‚.Exact := by
  constructor
  Â· rintro âŸ¨hâ‚, zâ‚âŸ©
    exact âŸ¨HomologyData.ofEpiOfIsIsoOfMono Ï† hâ‚, zâ‚âŸ©
  Â· rintro âŸ¨hâ‚‚, zâ‚‚âŸ©
    exact âŸ¨HomologyData.ofEpiOfIsIsoOfMono' Ï† hâ‚‚, zâ‚‚âŸ©

variable {S}

lemma HomologyData.exact_iff_i_p_zero (h : S.HomologyData) :
    S.Exact â†” h.left.i â‰« h.right.p = 0 := by
  haveI := HasHomology.mk' h
  rw [h.left.exact_iff, â† h.comm]
  constructor
  Â· intro z
    rw [IsZero.eq_of_src z h.iso.hom 0, zero_comp, comp_zero]
  Â· intro eq
    simp only [IsZero.iff_id_eq_zero, â† cancel_mono h.iso.hom, id_comp, â† cancel_mono h.right.Î¹,
      â† cancel_epi h.left.Ï€, eq, zero_comp, comp_zero]

variable (S)

lemma exact_iff_i_p_zero [S.HasHomology] (hâ‚ : S.LeftHomologyData)
    (hâ‚‚ : S.RightHomologyData) :
    S.Exact â†” hâ‚.i â‰« hâ‚‚.p = 0 :=
  (HomologyData.ofIsIsoLeftRightHomologyComparison' hâ‚ hâ‚‚).exact_iff_i_p_zero

lemma exact_iff_iCycles_pOpcycles_zero [S.HasHomology] :
    S.Exact â†” S.iCycles â‰« S.pOpcycles = 0 :=
  S.exact_iff_i_p_zero _ _

lemma exact_iff_kernel_Î¹_comp_cokernel_Ï€_zero [S.HasHomology]
    [HasKernel S.g] [HasCokernel S.f] :
    S.Exact â†” kernel.Î¹ S.g â‰« cokernel.Ï€ S.f = 0 := by
  haveI := HasLeftHomology.hasCokernel S
  haveI := HasRightHomology.hasKernel S
  exact S.exact_iff_i_p_zero (LeftHomologyData.ofHasKernelOfHasCokernel S)
    (RightHomologyData.ofHasCokernelOfHasKernel S)

/-- The notion of exactness given by `ShortComplex.Exact` is equivalent to
the one given by the previous API `CategoryTheory.Exact` in the case of
abelian categories. -/
lemma _root_.CategoryTheory.exact_iff_shortComplex_exact
    {A : Type*} [Category A] [Abelian A] (S : ShortComplex A) :
    CategoryTheory.Exact S.f S.g â†” S.Exact := by
  simp only [Abelian.exact_iff, S.zero,
    S.exact_iff_kernel_Î¹_comp_cokernel_Ï€_zero, true_and]

variable {S}

lemma Exact.op (h : S.Exact) : S.op.Exact := by
  obtain âŸ¨h, zâŸ© := h
  exact âŸ¨âŸ¨h.op, (IsZero.of_iso z h.iso.symm).opâŸ©âŸ©

lemma Exact.unop {S : ShortComplex Cáµ’áµ–} (h : S.Exact) : S.unop.Exact := by
  obtain âŸ¨h, zâŸ© := h
  exact âŸ¨âŸ¨h.unop, (IsZero.of_iso z h.iso.symm).unopâŸ©âŸ©

variable (S)

@[simp]
lemma exact_op_iff : S.op.Exact â†” S.Exact :=
  âŸ¨Exact.unop, Exact.opâŸ©

@[simp]
lemma exact_unop_iff (S : ShortComplex Cáµ’áµ–) : S.unop.Exact â†” S.Exact :=
  S.unop.exact_op_iff.symm

variable {S}

lemma LeftHomologyData.exact_map_iff (h : S.LeftHomologyData) (F : C â¥¤ D)
    [F.PreservesZeroMorphisms] [h.IsPreservedBy F] [(S.map F).HasHomology] :
    (S.map F).Exact â†” IsZero (F.obj h.H) :=
  (h.map F).exact_iff

lemma RightHomologyData.exact_map_iff (h : S.RightHomologyData) (F : C â¥¤ D)
    [F.PreservesZeroMorphisms] [h.IsPreservedBy F] [(S.map F).HasHomology] :
    (S.map F).Exact â†” IsZero (F.obj h.H) :=
  (h.map F).exact_iff

lemma Exact.map_of_preservesLeftHomologyOf (h : S.Exact) (F : C â¥¤ D)
    [F.PreservesZeroMorphisms] [F.PreservesLeftHomologyOf S]
    [(S.map F).HasHomology] : (S.map F).Exact := by
  have := h.hasHomology
  rw [S.leftHomologyData.exact_iff, IsZero.iff_id_eq_zero] at h
  rw [S.leftHomologyData.exact_map_iff F, IsZero.iff_id_eq_zero,
    â† F.map_id, h, F.map_zero]

lemma Exact.map_of_preservesRightHomologyOf (h : S.Exact) (F : C â¥¤ D)
    [F.PreservesZeroMorphisms] [F.PreservesRightHomologyOf S]
    [(S.map F).HasHomology] : (S.map F).Exact := by
  have : S.HasHomology := h.hasHomology
  rw [S.rightHomologyData.exact_iff, IsZero.iff_id_eq_zero] at h
  rw [S.rightHomologyData.exact_map_iff F, IsZero.iff_id_eq_zero,
    â† F.map_id, h, F.map_zero]

lemma Exact.map (h : S.Exact) (F : C â¥¤ D)
    [F.PreservesZeroMorphisms] [F.PreservesLeftHomologyOf S]
    [F.PreservesRightHomologyOf S] : (S.map F).Exact := by
  have := h.hasHomology
  exact h.map_of_preservesLeftHomologyOf F

variable (S)

lemma exact_map_iff_of_faithful [S.HasHomology]
    (F : C â¥¤ D) [F.PreservesZeroMorphisms] [F.PreservesLeftHomologyOf S]
    [F.PreservesRightHomologyOf S] [Faithful F] :
    (S.map F).Exact â†” S.Exact := by
  constructor
  Â· intro h
    rw [S.leftHomologyData.exact_iff, IsZero.iff_id_eq_zero]
    rw [(S.leftHomologyData.map F).exact_iff, IsZero.iff_id_eq_zero,
      LeftHomologyData.map_H] at h
    apply F.map_injective
    rw [F.map_id, F.map_zero, h]
  Â· intro h
    exact h.map F

variable {S}

@[reassoc]
lemma Exact.comp_eq_zero (h : S.Exact) {X Y : C} {a : X âŸ¶ S.Xâ‚‚} (ha : a â‰« S.g = 0)
    {b : S.Xâ‚‚ âŸ¶ Y} (hb : S.f â‰« b = 0) : a â‰« b = 0 := by
  have := h.hasHomology
  have eq := h
  rw [exact_iff_iCycles_pOpcycles_zero] at eq
  rw [â† S.liftCycles_i a ha, â† S.p_descOpcycles b hb, assoc, reassoc_of% eq,
    zero_comp, comp_zero]

lemma Exact.isZero_of_both_zeros (ex : S.Exact) (hf : S.f = 0) (hg : S.g = 0) :
    IsZero S.Xâ‚‚ :=
  (ShortComplex.HomologyData.ofZeros S hf hg).exact_iff.1 ex

end

section Preadditive

variable [Preadditive C] [Preadditive D] (S : ShortComplex C)

lemma exact_iff_mono [HasZeroObject C] (hf : S.f = 0) :
    S.Exact â†” Mono S.g := by
  constructor
  Â· intro h
    have := h.hasHomology
    simp only [exact_iff_isZero_homology] at h
    have := S.isIso_pOpcycles hf
    have := mono_of_isZero_kernel' _ S.homologyIsKernel h
    rw [â† S.p_fromOpcycles]
    apply mono_comp
  Â· intro
    rw [(HomologyData.ofIsLimitKernelFork S hf _
      (KernelFork.IsLimit.ofMonoOfIsZero (KernelFork.ofÎ¹ (0 : 0 âŸ¶ S.Xâ‚‚) zero_comp)
        inferInstance (isZero_zero C))).exact_iff]
    exact isZero_zero C

lemma exact_iff_epi [HasZeroObject C] (hg : S.g = 0) :
    S.Exact â†” Epi S.f := by
  constructor
  Â· intro h
    have := h.hasHomology
    simp only [exact_iff_isZero_homology] at h
    haveI := S.isIso_iCycles hg
    haveI : Epi S.toCycles := epi_of_isZero_cokernel' _ S.homologyIsCokernel h
    rw [â† S.toCycles_i]
    apply epi_comp
  Â· intro
    rw [(HomologyData.ofIsColimitCokernelCofork S hg _
      (CokernelCofork.IsColimit.ofEpiOfIsZero (CokernelCofork.ofÏ€ (0 : S.Xâ‚‚ âŸ¶ 0) comp_zero)
        inferInstance (isZero_zero C))).exact_iff]
    exact isZero_zero C

variable {S}

lemma Exact.epi_f' (hS : S.Exact) (h : LeftHomologyData S) : Epi h.f' :=
  epi_of_isZero_cokernel' _ h.hÏ€ (by
    haveI := hS.hasHomology
    dsimp
    simpa only [â† h.exact_iff] using hS)

lemma Exact.mono_g' (hS : S.Exact) (h : RightHomologyData S) : Mono h.g' :=
  mono_of_isZero_kernel' _ h.hÎ¹ (by
    haveI := hS.hasHomology
    dsimp
    simpa only [â† h.exact_iff] using hS)

lemma Exact.epi_toCycles (hS : S.Exact) [S.HasLeftHomology] : Epi S.toCycles :=
  hS.epi_f' _

lemma Exact.mono_fromOpcycles (hS : S.Exact) [S.HasRightHomology] : Mono S.fromOpcycles :=
  hS.mono_g' _

lemma LeftHomologyData.exact_iff_epi_f' [S.HasHomology] (h : LeftHomologyData S) :
    S.Exact â†” Epi h.f' := by
  constructor
  Â· intro hS
    exact hS.epi_f' h
  Â· intro
    simp only [h.exact_iff, IsZero.iff_id_eq_zero, â† cancel_epi h.Ï€, â† cancel_epi h.f',
      comp_id, h.f'_Ï€, comp_zero]

lemma RightHomologyData.exact_iff_mono_g' [S.HasHomology] (h : RightHomologyData S) :
    S.Exact â†” Mono h.g' := by
  constructor
  Â· intro hS
    exact hS.mono_g' h
  Â· intro
    simp only [h.exact_iff, IsZero.iff_id_eq_zero, â† cancel_mono h.Î¹, â† cancel_mono h.g',
      id_comp, h.Î¹_g', zero_comp]

/-- Given an exact short complex `S` and a limit kernel fork `kf` for `S.g`, this is the
left homology data for `S` with `K := kf.pt` and `H := 0`. -/
@[simps]
noncomputable def Exact.leftHomologyDataOfIsLimitKernelFork
    (hS : S.Exact) [HasZeroObject C] (kf : KernelFork S.g) (hkf : IsLimit kf) :
    S.LeftHomologyData where
  K := kf.pt
  H := 0
  i := kf.Î¹
  Ï€ := 0
  wi := kf.condition
  hi := IsLimit.ofIsoLimit hkf (Fork.ext (Iso.refl _) (by simp))
  wÏ€ := comp_zero
  hÏ€ := CokernelCofork.IsColimit.ofEpiOfIsZero _ (by
    have := hS.hasHomology
    refine' ((MorphismProperty.RespectsIso.epimorphisms C).arrow_mk_iso_iff _).1
      hS.epi_toCycles
    refine' Arrow.isoMk (Iso.refl _)
      (IsLimit.conePointUniqueUpToIso S.cyclesIsKernel hkf) _
    apply Fork.IsLimit.hom_ext hkf
    simp [IsLimit.conePointUniqueUpToIso]) (isZero_zero C)

/-- Given an exact short complex `S` and a colimit cokernel cofork `cc` for `S.f`, this is the
right homology data for `S` with `Q := cc.pt` and `H := 0`. -/
@[simps]
noncomputable def Exact.rightHomologyDataOfIsColimitCokernelCofork
    (hS : S.Exact) [HasZeroObject C] (cc : CokernelCofork S.f) (hcc : IsColimit cc) :
    S.RightHomologyData where
  Q := cc.pt
  H := 0
  p := cc.Ï€
  Î¹ := 0
  wp := cc.condition
  hp := IsColimit.ofIsoColimit hcc (Cofork.ext (Iso.refl _) (by simp))
  wÎ¹ := zero_comp
  hÎ¹ := KernelFork.IsLimit.ofMonoOfIsZero _ (by
    have := hS.hasHomology
    refine' ((MorphismProperty.RespectsIso.monomorphisms C).arrow_mk_iso_iff _).2
      hS.mono_fromOpcycles
    refine' Arrow.isoMk (IsColimit.coconePointUniqueUpToIso hcc S.opcyclesIsCokernel)
      (Iso.refl _) _
    apply Cofork.IsColimit.hom_ext hcc
    simp [IsColimit.coconePointUniqueUpToIso]) (isZero_zero C)

variable (S)

lemma exact_iff_epi_toCycles [S.HasHomology] : S.Exact â†” Epi S.toCycles :=
  S.leftHomologyData.exact_iff_epi_f'

lemma exact_iff_mono_fromOpcycles [S.HasHomology] : S.Exact â†” Mono S.fromOpcycles :=
  S.rightHomologyData.exact_iff_mono_g'

lemma exact_iff_epi_kernel_lift [S.HasHomology] [HasKernel S.g] :
    S.Exact â†” Epi (kernel.lift S.g S.f S.zero) := by
  rw [exact_iff_epi_toCycles]
  apply (MorphismProperty.RespectsIso.epimorphisms C).arrow_mk_iso_iff
  exact Arrow.isoMk (Iso.refl _) S.cyclesIsoKernel (by aesop_cat)

lemma exact_iff_mono_cokernel_desc [S.HasHomology] [HasCokernel S.f] :
    S.Exact â†” Mono (cokernel.desc S.f S.g S.zero) := by
  rw [exact_iff_mono_fromOpcycles]
  refine' (MorphismProperty.RespectsIso.monomorphisms C).arrow_mk_iso_iff (Iso.symm _)
  exact Arrow.isoMk S.opcyclesIsoCokernel.symm (Iso.refl _) (by aesop_cat)

lemma QuasiIso.exact_iff {Sâ‚ Sâ‚‚ : ShortComplex C} (Ï† : Sâ‚ âŸ¶ Sâ‚‚)
    [Sâ‚.HasHomology] [Sâ‚‚.HasHomology] [QuasiIso Ï†] : Sâ‚.Exact â†” Sâ‚‚.Exact := by
  simp only [exact_iff_isZero_homology]
  exact Iso.isZero_iff (asIso (homologyMap Ï†))

lemma exact_of_f_is_kernel (hS : IsLimit (KernelFork.ofÎ¹ S.f S.zero))
    [S.HasHomology] : S.Exact := by
  rw [exact_iff_epi_toCycles]
  have : IsSplitEpi S.toCycles :=
    âŸ¨âŸ¨{ section_ := hS.lift (KernelFork.ofÎ¹ S.iCycles S.iCycles_g)
        id := by
          rw [â† cancel_mono S.iCycles, assoc, toCycles_i, id_comp]
          exact Fork.IsLimit.lift_Î¹ hS }âŸ©âŸ©
  infer_instance

lemma exact_of_g_is_cokernel (hS : IsColimit (CokernelCofork.ofÏ€ S.g S.zero))
    [S.HasHomology] : S.Exact := by
  rw [exact_iff_mono_fromOpcycles]
  have : IsSplitMono S.fromOpcycles :=
    âŸ¨âŸ¨{ retraction := hS.desc (CokernelCofork.ofÏ€ S.pOpcycles S.f_pOpcycles)
        id := by
          rw [â† cancel_epi S.pOpcycles, p_fromOpcycles_assoc, comp_id]
          exact Cofork.IsColimit.Ï€_desc hS }âŸ©âŸ©
  infer_instance

/-- A splitting for a short complex `S` consists of the data of a retraction `r : Xâ‚‚ âŸ¶ Xâ‚`
of `S.f` and section `s : Xâ‚ƒ âŸ¶ Xâ‚‚` of `S.g` which satisfy `r â‰« S.f + S.g â‰« s = ğŸ™ _` -/
structure Splitting (S : ShortComplex C) where
  /-- a retraction of `S.f` -/
  r : S.Xâ‚‚ âŸ¶ S.Xâ‚
  /-- a section of `S.g` -/
  s : S.Xâ‚ƒ âŸ¶ S.Xâ‚‚
  /-- the condition that `r` is a retraction of `S.f` -/
  f_r : S.f â‰« r = ğŸ™ _ := by aesop_cat
  /-- the condition that `s` is a section of `S.g` -/
  s_g : s â‰« S.g = ğŸ™ _ := by aesop_cat
  /-- the compatibility between the given section and retraction -/
  id : r â‰« S.f + S.g â‰« s = ğŸ™ _ := by aesop_cat

namespace Splitting

attribute [reassoc (attr := simp)] f_r s_g

variable {S}

@[reassoc]
lemma r_f (s : S.Splitting) : s.r â‰« S.f = ğŸ™ _ - S.g â‰« s.s := by rw [â† s.id, add_sub_cancel]

@[reassoc]
lemma g_s (s : S.Splitting) : S.g â‰« s.s = ğŸ™ _ - s.r â‰« S.f := by rw [â† s.id, add_sub_cancel']

/-- Given a splitting of a short complex `S`, this shows that `S.f` is a split monomorphism. -/
@[simps] def splitMono_f (s : S.Splitting) : SplitMono S.f := âŸ¨s.r, s.f_râŸ©

lemma isSplitMono_f (s : S.Splitting) : IsSplitMono S.f := âŸ¨âŸ¨s.splitMono_fâŸ©âŸ©

lemma mono_f (s : S.Splitting) : Mono S.f := by
  have := s.isSplitMono_f
  infer_instance

/-- Given a splitting of a short complex `S`, this shows that `S.g` is a split epimorphism. -/
@[simps] def splitEpi_g (s : S.Splitting) : SplitEpi S.g := âŸ¨s.s, s.s_gâŸ©

lemma isSplitEpi_g (s : S.Splitting) : IsSplitEpi S.g := âŸ¨âŸ¨s.splitEpi_gâŸ©âŸ©

lemma epi_g (s : S.Splitting) : Epi S.g := by
  have := s.isSplitEpi_g
  infer_instance

@[reassoc (attr := simp)]
lemma s_r (s : S.Splitting) : s.s â‰« s.r = 0 := by
  have := s.epi_g
  simp only [â† cancel_epi S.g, comp_zero, g_s_assoc, sub_comp, id_comp,
    assoc, f_r, comp_id, sub_self]

lemma ext_r (s s' : S.Splitting) (h : s.r = s'.r) : s = s' := by
  have := s.epi_g
  have eq := s.id
  rw [â† s'.id, h, add_right_inj, cancel_epi S.g] at eq
  cases s
  cases s'
  obtain rfl := eq
  obtain rfl := h
  rfl

lemma ext_s (s s' : S.Splitting) (h : s.s = s'.s) : s = s' := by
  have := s.mono_f
  have eq := s.id
  rw [â† s'.id, h, add_left_inj, cancel_mono S.f] at eq
  cases s
  cases s'
  obtain rfl := eq
  obtain rfl := h
  rfl

/-- The left homology data on a short complex equipped with a splitting. -/
@[simps]
noncomputable def leftHomologyData [HasZeroObject C] (s : S.Splitting) :
    LeftHomologyData S := by
  have hi := KernelFork.IsLimit.ofÎ¹ S.f S.zero
    (fun x _ => x â‰« s.r)
    (fun x hx => by simp only [assoc, s.r_f, comp_sub, comp_id,
      sub_eq_self, reassoc_of% hx, zero_comp])
    (fun x _ b hb => by simp only [â† hb, assoc, f_r, comp_id])
  let f' := hi.lift (KernelFork.ofÎ¹ S.f S.zero)
  have hf' : f' = ğŸ™ _ := by
    apply Fork.IsLimit.hom_ext hi
    dsimp
    erw [Fork.IsLimit.lift_Î¹ hi]
    simp only [Fork.Î¹_ofÎ¹, id_comp]
  have wÏ€ : f' â‰« (0 : S.Xâ‚ âŸ¶ 0) = 0 := comp_zero
  have hÏ€ : IsColimit (CokernelCofork.ofÏ€ 0 wÏ€) := CokernelCofork.IsColimit.ofEpiOfIsZero _
      (by rw [hf'] ; infer_instance) (isZero_zero _)
  exact
    { K := S.Xâ‚
      H := 0
      i := S.f
      wi := S.zero
      hi := hi
      Ï€ := 0
      wÏ€ := wÏ€
      hÏ€ := hÏ€ }

/-- The right homology data on a short complex equipped with a splitting. -/
@[simps]
noncomputable def rightHomologyData [HasZeroObject C] (s : S.Splitting) :
    RightHomologyData S := by
  have hp := CokernelCofork.IsColimit.ofÏ€ S.g S.zero
    (fun x _ => s.s â‰« x)
    (fun x hx => by simp only [s.g_s_assoc, sub_comp, id_comp, sub_eq_self, assoc, hx, comp_zero])
    (fun x _ b hb => by simp only [â† hb, s.s_g_assoc])
  let g' := hp.desc (CokernelCofork.ofÏ€ S.g S.zero)
  have hg' : g' = ğŸ™ _ := by
    apply Cofork.IsColimit.hom_ext hp
    dsimp
    erw [Cofork.IsColimit.Ï€_desc hp]
    simp only [Cofork.Ï€_ofÏ€, comp_id]
  have wÎ¹ : (0 : 0 âŸ¶ S.Xâ‚ƒ) â‰« g' = 0 := zero_comp
  have hÎ¹ : IsLimit (KernelFork.ofÎ¹ 0 wÎ¹) := KernelFork.IsLimit.ofMonoOfIsZero _
      (by rw [hg'] ; dsimp ; infer_instance) (isZero_zero _)
  exact
    { Q := S.Xâ‚ƒ
      H := 0
      p := S.g
      wp := S.zero
      hp := hp
      Î¹ := 0
      wÎ¹ := wÎ¹
      hÎ¹ := hÎ¹ }

/-- The homology data on a short complex equipped with a splitting. -/
@[simps]
noncomputable def homologyData [HasZeroObject C] (s : S.Splitting) : S.HomologyData where
  left := s.leftHomologyData
  right := s.rightHomologyData
  iso := Iso.refl 0

/-- A short complex equipped with a splitting is exact. -/
lemma exact [HasZeroObject C] (s : S.Splitting) : S.Exact :=
  âŸ¨s.homologyData, isZero_zero _âŸ©

end Splitting

end Preadditive

end ShortComplex

end CategoryTheory
