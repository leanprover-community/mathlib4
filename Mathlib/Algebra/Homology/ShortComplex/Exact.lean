import Mathlib.Algebra.Homology.ShortComplex.QuasiIso
import Mathlib.Algebra.Homology.ShortComplex.Preadditive
import Mathlib.Algebra.Homology.ShortComplex.PreservesHomology
import Mathlib.CategoryTheory.Balanced

namespace CategoryTheory

open Category Limits ZeroObject Preadditive

variable {C D : Type _} [Category C] [Category D]

namespace ShortComplex

section

variable
  [HasZeroMorphisms C] [HasZeroMorphisms D]
  (S : ShortComplex C) {Sâ‚ Sâ‚‚ : ShortComplex C}

structure Exact : Prop :=
  condition : âˆƒ (h : S.HomologyData), IsZero h.left.H

variable {S}

lemma Exact.hasHomology (h : S.Exact) : S.HasHomology :=
  HasHomology.mk' h.condition.choose

variable (S)

lemma exact_iff_isZero_homology [S.HasHomology] :
    S.Exact â†” IsZero S.homology := by
  constructor
  . rintro âŸ¨âŸ¨h', zâŸ©âŸ©
    exact IsZero.of_iso z h'.left.homologyIso
  . intro h
    exact âŸ¨âŸ¨_, hâŸ©âŸ©

variable {S}

lemma LeftHomologyData.exact_iff [S.HasHomology]
    (h : S.LeftHomologyData) :
    S.Exact â†” IsZero h.H := by
  rw [S.exact_iff_isZero_homology]
  exact Iso.isZero_iff h.homologyIso

lemma RightHomologyData.exact_iff [S.HasHomology]
    (h : S.RightHomologyData) :
    S.Exact â†” IsZero h.H := by
  rw [S.exact_iff_isZero_homology]
  exact Iso.isZero_iff h.homologyIso

variable (S)

lemma exact_iff_isZero_leftHomology [S.HasHomology] :
    S.Exact â†” IsZero S.leftHomology :=
  LeftHomologyData.exact_iff _

lemma exact_iff_isZero_right_homology [S.HasHomology] :
    S.Exact â†” IsZero S.rightHomology :=
  RightHomologyData.exact_iff _

variable {S}

lemma HomologyData.exact_iff (h : S.HomologyData) :
    S.Exact â†” IsZero h.left.H := by
  haveI := HasHomology.mk' h
  exact LeftHomologyData.exact_iff h.left

lemma HomologyData.exact_iff' (h : S.HomologyData) :
    S.Exact â†” IsZero h.right.H := by
  haveI := HasHomology.mk' h
  exact RightHomologyData.exact_iff h.right

variable (S)

lemma exact_iff_homology_iso_zero [S.HasHomology] [HasZeroObject C] :
    S.Exact â†” Nonempty (S.homology â‰… 0) := by
  rw [exact_iff_isZero_homology]
  constructor
  . intro h
    exact âŸ¨h.isoZeroâŸ©
  . rintro âŸ¨eâŸ©
    exact IsZero.of_iso (isZero_zero C) e

lemma exact_of_iso (e : Sâ‚ â‰… Sâ‚‚) (h : Sâ‚.Exact) : Sâ‚‚.Exact := by
  obtain âŸ¨âŸ¨h, zâŸ©âŸ© := h
  exact âŸ¨âŸ¨HomologyData.ofIso e h, zâŸ©âŸ©

lemma exact_iff_of_iso (e : Sâ‚ â‰… Sâ‚‚) : Sâ‚.Exact â†” Sâ‚‚.Exact :=
  âŸ¨exact_of_iso e, exact_of_iso e.symmâŸ©

lemma exact_of_isZero_Xâ‚‚ (h : IsZero S.Xâ‚‚) : S.Exact := by
  rw [(HomologyData.ofZeros S (IsZero.eq_of_tgt h _ _) (IsZero.eq_of_src h _ _)).exact_iff]
  exact h

lemma exact_iff_of_epi_of_isIso_of_mono (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [Epi Ï†.Ï„â‚] [IsIso Ï†.Ï„â‚‚] [Mono Ï†.Ï„â‚ƒ] :
    Sâ‚.Exact â†” Sâ‚‚.Exact := by
  constructor
  . rintro âŸ¨hâ‚, zâ‚âŸ©
    exact âŸ¨HomologyData.ofEpiOfIsIsoOfMono Ï† hâ‚, zâ‚âŸ©
  . rintro âŸ¨hâ‚‚, zâ‚‚âŸ©
    exact âŸ¨HomologyData.ofEpiOfIsIsoOfMono' Ï† hâ‚‚, zâ‚‚âŸ©

variable {S}

lemma HomologyData.exact_iff_i_p_zero (h : S.HomologyData) :
    S.Exact â†” h.left.i â‰« h.right.p = 0 := by
  haveI := HasHomology.mk' h
  rw [h.left.exact_iff, â† h.comm]
  constructor
  . intro z
    rw [IsZero.eq_of_src z h.iso.hom 0, zero_comp, comp_zero]
  . intro eq
    simp only [IsZero.iff_id_eq_zero, â† cancel_mono h.iso.hom, id_comp, â† cancel_mono h.right.Î¹,
      â† cancel_epi h.left.Ï€, eq, zero_comp, comp_zero]

variable (S)

lemma exact_iff_i_p_zero [S.HasHomology] (hâ‚ : S.LeftHomologyData)
    (hâ‚‚ : S.RightHomologyData) :
    S.Exact â†” hâ‚.i â‰« hâ‚‚.p = 0 :=
  (HomologyData.ofIsIsoLeftRightHomologyComparison' hâ‚ hâ‚‚).exact_iff_i_p_zero

lemma exact_iff_iCycles_pCyclesCo_zero [S.HasHomology] :
    S.Exact â†” S.iCycles â‰« S.pCyclesCo = 0 :=
  S.exact_iff_i_p_zero _ _

lemma exact_iff_kernel_Î¹_comp_cokernel_Ï€_zero [S.HasHomology]
    [HasKernel S.g] [HasCokernel S.f] :
    S.Exact â†” kernel.Î¹ S.g â‰« cokernel.Ï€ S.f = 0 := by
  haveI : HasCokernel _ := HasLeftHomology.hasCokernel S
  haveI : HasKernel _ := HasRightHomology.hasKernel S
  exact S.exact_iff_i_p_zero (LeftHomologyData.ofKerOfCoker S)
    (RightHomologyData.ofKerOfCoker S)

variable {S}

lemma Exact.op (h : S.Exact) : S.op.Exact := by
  obtain âŸ¨h, zâŸ© := h
  exact âŸ¨âŸ¨h.op, (IsZero.of_iso z h.iso.symm).opâŸ©âŸ©

lemma Exact.unop {S : ShortComplex Cáµ’áµ–} (h : S.Exact) : S.unop.Exact := by
  obtain âŸ¨h, zâŸ© := h
  exact âŸ¨âŸ¨h.unop, (IsZero.of_iso z h.iso.symm).unopâŸ©âŸ©

variable (S)

lemma exact_iff_op : S.Exact â†” S.op.Exact :=
  âŸ¨Exact.op, Exact.unopâŸ©

lemma exact_iff_unop (S : ShortComplex Cáµ’áµ–) : S.Exact â†” S.unop.Exact :=
  S.unop.exact_iff_op.symm

variable {S}

lemma LeftHomologyData.exact_map_iff (h : S.LeftHomologyData) (F : C â¥¤ D)
    [F.PreservesZeroMorphisms] [h.IsPreservedBy F] [(S.map F).HasHomology] :
    (S.map F).Exact â†” IsZero (F.obj h.H) :=
    (h.map F).exact_iff

lemma RightHomologyData.exact_map_iff (h : S.RightHomologyData) (F : C â¥¤ D)
    [F.PreservesZeroMorphisms] [h.IsPreservedBy F] [(S.map F).HasHomology] :
    (S.map F).Exact â†” IsZero (F.obj h.H) :=
    (h.map F).exact_iff

lemma Exact.map_of_preservesLeftHomologyOf (h : S.Exact) (F : C â¥¤ D)
    [F.PreservesZeroMorphisms] [F.PreservesLeftHomologyOf S]
    [(S.map F).HasHomology] : (S.map F).Exact := by
    have := h.hasHomology
    rw [(S.leftHomologyData).exact_iff, IsZero.iff_id_eq_zero] at h
    rw [(S.leftHomologyData).exact_map_iff F, IsZero.iff_id_eq_zero,
      â† F.map_id, h, F.map_zero]

lemma Exact.map_of_preservesRightHomologyOf (h : S.Exact) (F : C â¥¤ D)
    [F.PreservesZeroMorphisms] [F.PreservesRightHomologyOf S]
    [(S.map F).HasHomology] : (S.map F).Exact := by
    have : S.HasHomology := h.hasHomology
    rw [(S.rightHomologyData).exact_iff, IsZero.iff_id_eq_zero] at h
    rw [(S.rightHomologyData).exact_map_iff F, IsZero.iff_id_eq_zero,
      â† F.map_id, h, F.map_zero]

lemma Exact.map (h : S.Exact) (F : C â¥¤ D)
    [F.PreservesZeroMorphisms] [F.PreservesLeftHomologyOf S]
    [F.PreservesRightHomologyOf S] : (S.map F).Exact := by
    have := h.hasHomology
    exact h.map_of_preservesLeftHomologyOf F

variable (S)

lemma exact_map_iff_of_faithful [S.HasHomology]
    (F : C â¥¤ D) [F.PreservesZeroMorphisms] [F.PreservesLeftHomologyOf S]
    [F.PreservesRightHomologyOf S] [Faithful F] :
    (S.map F).Exact â†” S.Exact := by
    constructor
    . intro h
      rw [S.leftHomologyData.exact_iff, IsZero.iff_id_eq_zero]
      rw [(S.leftHomologyData.map F).exact_iff, IsZero.iff_id_eq_zero,
        LeftHomologyData.map_H] at h
      apply F.map_injective
      rw [F.map_id, F.map_zero, h]
    . intro h
      exact h.map F


variable {S}

lemma Exact.comp_eq_zero (h : S.Exact) {X Y : C} {a : X âŸ¶ S.Xâ‚‚} (ha : a â‰« S.g = 0)
  {b : S.Xâ‚‚ âŸ¶ Y} (hb : S.f â‰« b = 0) : a â‰« b = 0 := by
    have := h.hasHomology
    have eq := h
    rw [exact_iff_iCycles_pCyclesCo_zero] at eq
    rw [â† S.liftCycles_i a ha, â† S.p_descCyclesCo b hb, assoc, reassoc_of% eq,
      zero_comp, comp_zero]

lemma Exact.isZero_of_both_zeros (ex : S.Exact) (hf : S.f = 0) (hg : S.g = 0) :
    IsZero S.Xâ‚‚ :=
  (ShortComplex.HomologyData.ofZeros S hf hg).exact_iff.1 ex

end

section Preadditive

variable [Preadditive C] [Preadditive D] (S : ShortComplex C)

lemma exact_iff_mono [HasZeroObject C] (hf : S.f = 0) :
    S.Exact â†” Mono S.g := by
  constructor
  . intro h
    have : S.HasHomology := HasHomology.mk' h.condition.choose
    simp only [exact_iff_isZero_homology] at h
    have := S.isIso_pCyclesCo_of_zero hf
    have := mono_of_isZero_kernel' _ S.homologyIsKernel h
    rw [â† S.p_fromCyclesCo]
    apply mono_comp
  . intro
    rw [(HomologyData.ofIsLimitKernelFork S hf _
      (KernelFork.IsLimit.ofIsZeroOfMono (KernelFork.ofÎ¹ (0 : 0 âŸ¶ S.Xâ‚‚) zero_comp)
        inferInstance (isZero_zero C))).exact_iff]
    exact isZero_zero C

lemma exact_iff_epi [HasZeroObject C] (hg : S.g = 0) :
    S.Exact â†” Epi S.f := by
  constructor
  . intro h
    haveI : S.HasHomology := HasHomology.mk' h.condition.choose
    simp only [exact_iff_isZero_homology] at h
    haveI := S.isIso_iCycles_of_zero hg
    haveI : Epi S.toCycles := epi_of_isZero_cokernel' _ S.homologyIsCokernel h
    rw [â† S.toCycles_i]
    apply epi_comp
  . intro
    rw [(HomologyData.ofIsColimitCokernelCofork S hg _
      (CokernelCofork.IsColimit.ofIsZeroOfEpi (CokernelCofork.ofÏ€ (0 : S.Xâ‚‚ âŸ¶ 0) comp_zero)
        inferInstance (isZero_zero C))).exact_iff]
    exact isZero_zero C

variable {S}

lemma Exact.epi_f' (hS : S.Exact) (h : LeftHomologyData S) : Epi h.f' :=
  epi_of_isZero_cokernel' _ h.hÏ€ (by
    haveI := hS.hasHomology
    dsimp
    simpa only [â† h.exact_iff] using hS)

lemma Exact.mono_g' (hS : S.Exact) (h : RightHomologyData S) : Mono h.g' :=
  mono_of_isZero_kernel' _ h.hÎ¹ (by
    haveI := hS.hasHomology
    dsimp
    simpa only [â† h.exact_iff] using hS)

lemma Exact.epi_toCycles (hS : S.Exact) [S.HasLeftHomology] : Epi S.toCycles :=
  hS.epi_f' _

lemma Exact.mono_fromCyclesCo (hS : S.Exact) [S.HasRightHomology] : Mono S.fromCyclesCo :=
  hS.mono_g' _

lemma LeftHomologyData.exact_iff_epi_f' [S.HasHomology] (h : LeftHomologyData S) :
    S.Exact â†” Epi h.f' := by
  constructor
  . intro hS
    exact hS.epi_f' h
  . intro
    simp only [h.exact_iff, IsZero.iff_id_eq_zero, â† cancel_epi h.Ï€, â† cancel_epi h.f',
      comp_id, h.f'_Ï€, comp_zero]

lemma RightHomologyData.exact_iff_mono_g' [S.HasHomology] (h : RightHomologyData S) :
    S.Exact â†” Mono h.g' := by
  constructor
  . intro hS
    exact hS.mono_g' h
  . intro
    simp only [h.exact_iff, IsZero.iff_id_eq_zero, â† cancel_mono h.Î¹, â† cancel_mono h.g',
      id_comp, h.Î¹_g', zero_comp]

variable (S)

lemma exact_iff_epi_toCycles [S.HasHomology] : S.Exact â†” Epi S.toCycles :=
  S.leftHomologyData.exact_iff_epi_f'

lemma exact_iff_mono_fromCyclesCo [S.HasHomology] : S.Exact â†” Mono S.fromCyclesCo :=
  S.rightHomologyData.exact_iff_mono_g'

lemma exact_iff_epi_kernel_lift [S.HasHomology] [HasKernel S.g] :
    S.Exact â†” Epi (kernel.lift S.g S.f S.zero) := by
  rw [exact_iff_epi_toCycles]
  have eqâ‚ : kernel.lift S.g S.f S.zero = S.toCycles â‰« S.cyclesIsoKernel.hom := by
    simp only [cyclesIsoKernel_hom, â† cancel_mono (kernel.Î¹ S.g), kernel.lift_Î¹,
      assoc, toCycles_i]
  have eqâ‚‚ : S.toCycles = kernel.lift S.g S.f S.zero â‰« S.cyclesIsoKernel.inv := by
    rw [eqâ‚, assoc, Iso.hom_inv_id, comp_id]
  constructor
  . intro
    rw [eqâ‚]
    apply epi_comp
  . intro
    rw [eqâ‚‚]
    apply epi_comp

lemma exact_iff_mono_cokernel_desc [S.HasHomology] [HasCokernel S.f] :
    S.Exact â†” Mono (cokernel.desc S.f S.g S.zero) := by
  rw [exact_iff_mono_fromCyclesCo]
  have eqâ‚ : cokernel.desc S.f S.g S.zero = S.cyclesCoIsoCokernel.inv â‰« S.fromCyclesCo := by
    simp only [â† cancel_epi (cokernel.Ï€ S.f), cokernel.Ï€_desc, cyclesCoIsoCokernel_inv,
      cokernel.Ï€_desc_assoc, p_fromCyclesCo]
  have eqâ‚‚ : S.fromCyclesCo = S.cyclesCoIsoCokernel.hom â‰« cokernel.desc S.f S.g S.zero := by
    rw [eqâ‚, Iso.hom_inv_id_assoc]
  constructor
  . intro
    rw [eqâ‚]
    apply mono_comp
  . intro
    rw [eqâ‚‚]
    apply mono_comp

lemma QuasiIso.exact_iff {Sâ‚ Sâ‚‚ : ShortComplex C} (Ï† : Sâ‚ âŸ¶ Sâ‚‚)
    [Sâ‚.HasHomology] [Sâ‚‚.HasHomology] [QuasiIso Ï†] : Sâ‚.Exact â†” Sâ‚‚.Exact := by
  simp only [exact_iff_isZero_homology]
  exact Iso.isZero_iff (QuasiIso.iso Ï†)

lemma HomotopyEquiv.exact_iff {Sâ‚ Sâ‚‚ : ShortComplex C} (e : HomotopyEquiv Sâ‚ Sâ‚‚)
    [Sâ‚.HasHomology] [Sâ‚‚.HasHomology] : Sâ‚.Exact â†” Sâ‚‚.Exact :=
  QuasiIso.exact_iff e.hom

lemma exact_of_f_is_kernel (hS : IsLimit (KernelFork.ofÎ¹ S.f S.zero))
    [S.HasHomology] : S.Exact := by
  rw [exact_iff_epi_toCycles]
  have : IsSplitEpi S.toCycles :=
    âŸ¨âŸ¨{ section_ := hS.lift (KernelFork.ofÎ¹ S.iCycles S.iCycles_g)
        id := by
          rw [â† cancel_mono S.iCycles]
          simp only [assoc, toCycles_i, id_comp]
          exact Fork.IsLimit.lift_Î¹ hS }âŸ©âŸ©
  infer_instance

lemma exact_of_g_is_cokernel (hS : IsColimit (CokernelCofork.ofÏ€ S.g S.zero))
    [S.HasHomology] : S.Exact := by
  rw [exact_iff_mono_fromCyclesCo]
  have : IsSplitMono S.fromCyclesCo :=
    âŸ¨âŸ¨{ retraction := hS.desc (CokernelCofork.ofÏ€ S.pCyclesCo S.f_pCyclesCo)
        id := by
          rw [â† cancel_epi S.pCyclesCo]
          simp only [assoc, p_fromCyclesCo_assoc, comp_id]
          exact Cofork.IsColimit.Ï€_desc hS }âŸ©âŸ©
  infer_instance

structure Splitting (S : ShortComplex C) where
  r : S.Xâ‚‚ âŸ¶ S.Xâ‚
  s : S.Xâ‚ƒ âŸ¶ S.Xâ‚‚
  f_r : S.f â‰« r = ğŸ™ _ := by aesop_cat
  s_g : s â‰« S.g = ğŸ™ _ := by aesop_cat
  id : r â‰« S.f + S.g â‰« s = ğŸ™ _ := by aesop_cat

namespace Splitting

attribute [reassoc (attr := simp)] f_r s_g

variable {S}

@[reassoc]
lemma r_f (s : S.Splitting) : s.r â‰« S.f = ğŸ™ _ - S.g â‰« s.s := by rw [â† s.id, add_sub_cancel]

@[reassoc]
lemma g_s (s : S.Splitting) : S.g â‰« s.s = ğŸ™ _ - s.r â‰« S.f := by rw [â† s.id, add_sub_cancel']

@[simps] def splitMono_f (s : S.Splitting) : SplitMono S.f := âŸ¨s.r, s.f_râŸ©
lemma isSplitMono_f (s : S.Splitting) : IsSplitMono S.f := âŸ¨âŸ¨s.splitMono_fâŸ©âŸ©
lemma mono_f (s : S.Splitting) : Mono S.f := by
  have := s.isSplitMono_f
  infer_instance

@[simps] def splitEpi_g (s : S.Splitting) : SplitEpi S.g := âŸ¨s.s, s.s_gâŸ©
lemma isSplitEpi_g (s : S.Splitting) : IsSplitEpi S.g := âŸ¨âŸ¨s.splitEpi_gâŸ©âŸ©
lemma epi_g (s : S.Splitting) : Epi S.g := by
  have := s.isSplitEpi_g
  infer_instance

lemma ext_r (s s' : S.Splitting) (h : s.r = s'.r) : s = s' := by
  have := s.epi_g
  have eq : ğŸ™ S.Xâ‚‚ = ğŸ™ S.Xâ‚‚ := rfl
  nth_rw 1 [â† s.id] at eq
  rw [â† s'.id, h, add_right_inj, cancel_epi S.g] at eq
  cases s
  cases s'
  obtain rfl := eq
  obtain rfl := h
  rfl

lemma ext_s (s s' : S.Splitting) (h : s.s = s'.s) : s = s' := by
  have := s.mono_f
  have eq : ğŸ™ S.Xâ‚‚ = ğŸ™ S.Xâ‚‚ := rfl
  nth_rw 1 [â† s.id] at eq
  rw [â† s'.id, h, add_left_inj, cancel_mono S.f] at eq
  cases s
  cases s'
  obtain rfl := eq
  obtain rfl := h
  rfl

@[simp]
noncomputable def leftHomologyData [HasZeroObject C] (s : S.Splitting) :
    LeftHomologyData S := by
  have hi := KernelFork.IsLimit.ofÎ¹ S.f S.zero
    (fun x _ => x â‰« s.r)
    (fun x hx => by simp only [assoc, s.r_f, comp_sub, comp_id,
      sub_eq_self, reassoc_of% hx, zero_comp])
    (fun x _ b hb => by simp only [â† hb, assoc, f_r, comp_id])
  let f' := hi.lift (KernelFork.ofÎ¹ S.f S.zero)
  have hf' : f' = ğŸ™ _ := by
    apply Fork.IsLimit.hom_ext hi
    dsimp
    erw [Fork.IsLimit.lift_Î¹ hi]
    simp only [Fork.Î¹_ofÎ¹, id_comp]
  have wÏ€ : f' â‰« (0 : S.Xâ‚ âŸ¶ 0) = 0 := comp_zero
  have hÏ€ : IsColimit (CokernelCofork.ofÏ€ 0 wÏ€) := CokernelCofork.IsColimit.ofIsZeroOfEpi _
      (by rw [hf'] ; infer_instance) (isZero_zero _)
  exact
  { K := S.Xâ‚
    H := 0
    i := S.f
    wi := S.zero
    hi := hi
    Ï€ := 0
    wÏ€ := wÏ€
    hÏ€ := hÏ€ }

@[simp]
noncomputable def rightHomologyData [HasZeroObject C] (s : S.Splitting) :
    RightHomologyData S := by
  have hp := CokernelCofork.IsColimit.ofÏ€ S.g S.zero
    (fun x _ => s.s â‰« x)
    (fun x hx => by simp only [s.g_s_assoc, sub_comp, id_comp, sub_eq_self, assoc, hx, comp_zero])
    (fun x _ b hb => by simp only [â† hb, s.s_g_assoc])
  let g' := hp.desc (CokernelCofork.ofÏ€ S.g S.zero)
  have hg' : g' = ğŸ™ _ := by
    apply Cofork.IsColimit.hom_ext hp
    dsimp
    erw [Cofork.IsColimit.Ï€_desc hp]
    simp only [Cofork.Ï€_ofÏ€, comp_id]
  have wÎ¹ : (0 : 0 âŸ¶ S.Xâ‚ƒ) â‰« g' = 0 := zero_comp
  have hÎ¹ : IsLimit (KernelFork.ofÎ¹ 0 wÎ¹) := KernelFork.IsLimit.ofIsZeroOfMono _
      (by rw [hg'] ; dsimp ; infer_instance) (isZero_zero _)
  exact
  { Q := S.Xâ‚ƒ
    H := 0
    p := S.g
    wp := S.zero
    hp := hp
    Î¹ := 0
    wÎ¹ := wÎ¹
    hÎ¹ := hÎ¹ }

@[simps]
noncomputable def homologyData [HasZeroObject C] (s : S.Splitting) : S.HomologyData where
  left := s.leftHomologyData
  right := s.rightHomologyData
  iso := Iso.refl 0

lemma exact [HasZeroObject C] (s : S.Splitting) : S.Exact :=
  âŸ¨s.homologyData, isZero_zero _âŸ©

@[simps]
def map (s : S.Splitting) (F : C â¥¤ D) [F.Additive] : (S.map F).Splitting where
  r := F.map s.r
  s := F.map s.s
  f_r := by
    dsimp [ShortComplex.map]
    rw [â† F.map_comp, f_r, F.map_id]
  s_g := by
    dsimp [ShortComplex.map]
    simp only [â† F.map_comp, s_g, F.map_id]
  id := by
    dsimp [ShortComplex.map]
    simp only [â† F.map_id, â† s.id, Functor.map_comp, Functor.map_add]

@[simps]
def ofIso {Sâ‚ Sâ‚‚ : ShortComplex C} (s : Sâ‚.Splitting) (e : Sâ‚ â‰… Sâ‚‚) : Sâ‚‚.Splitting where
  r := e.inv.Ï„â‚‚ â‰« s.r â‰« e.hom.Ï„â‚
  s := e.inv.Ï„â‚ƒ â‰« s.s â‰« e.hom.Ï„â‚‚
  f_r := by rw [â† e.inv.commâ‚â‚‚_assoc, s.f_r_assoc, â† comp_Ï„â‚, e.inv_hom_id, id_Ï„â‚]
  s_g := by rw [assoc, assoc, e.hom.commâ‚‚â‚ƒ, s.s_g_assoc, â† comp_Ï„â‚ƒ, e.inv_hom_id, id_Ï„â‚ƒ]
  id := by
    have eq := e.inv.Ï„â‚‚ â‰«= s.id =â‰« e.hom.Ï„â‚‚
    rw [id_comp, â† comp_Ï„â‚‚, e.inv_hom_id, id_Ï„â‚‚] at eq
    rw [â† eq, assoc, assoc, add_comp, assoc, assoc, comp_add,
      e.hom.commâ‚â‚‚, e.inv.commâ‚‚â‚ƒ_assoc]

end Splitting

section Balanced

variable {S}
variable [Balanced C]

namespace Exact

variable (hS : S.Exact)

lemma isIso_f' (h : S.LeftHomologyData) [Mono S.f] :
    IsIso h.f' := by
    have := hS.epi_f' h
    have := mono_of_mono_fac h.f'_i
    exact isIso_of_mono_of_epi h.f'

lemma isIso_toCycles [Mono S.f] [S.HasLeftHomology] :
    IsIso S.toCycles := by
    exact hS.isIso_f' _

lemma isIso_g' (h : S.RightHomologyData) [Epi S.g] :
    IsIso h.g' := by
    have := hS.mono_g' h
    have := epi_of_epi_fac h.p_g'
    exact isIso_of_mono_of_epi h.g'

lemma isIso_fromCyclesCo [Epi S.g] [S.HasRightHomology] :
    IsIso S.fromCyclesCo := by
    exact hS.isIso_g' _

noncomputable def fIsKernel [Mono S.f] : IsLimit (KernelFork.ofÎ¹ S.f S.zero) := by
  have := hS.hasHomology
  have := hS.isIso_toCycles
  exact IsLimit.ofIsoLimit S.cyclesIsKernel
    (Iso.symm (Fork.ext (asIso S.toCycles) (by simp)))

noncomputable def gIsCokernel [Epi S.g] : IsColimit (CokernelCofork.ofÏ€ S.g S.zero) := by
  have := hS.hasHomology
  have := hS.isIso_fromCyclesCo
  exact IsColimit.ofIsoColimit S.cyclesCoIsCokernel
    ((Cofork.ext (asIso S.fromCyclesCo) (by simp)))

noncomputable def lift {A : C} (k : A âŸ¶ S.Xâ‚‚) (hk : k â‰« S.g = 0) [Mono S.f] :
    A âŸ¶ S.Xâ‚ := hS.fIsKernel.lift (KernelFork.ofÎ¹ k hk)

@[reassoc (attr := simp)]
lemma lift_f {A : C} (k : A âŸ¶ S.Xâ‚‚) (hk : k â‰« S.g = 0) [Mono S.f] :
    hS.lift k hk â‰« S.f = k :=
  Fork.IsLimit.lift_Î¹ _

noncomputable def desc {A : C} (k : S.Xâ‚‚ âŸ¶ A) (hk : S.f â‰« k = 0) [Epi S.g] :
    S.Xâ‚ƒ âŸ¶ A := hS.gIsCokernel.desc (CokernelCofork.ofÏ€ k hk)

@[reassoc (attr := simp)]
lemma g_desc {A : C} (k : S.Xâ‚‚ âŸ¶ A) (hk : S.f â‰« k = 0) [Epi S.g] :
    S.g â‰« hS.desc k hk = k :=
  Cofork.IsColimit.Ï€_desc (hS.gIsCokernel)

end Exact

lemma mono_Ï„â‚‚_of_exact_of_mono {Sâ‚ Sâ‚‚ : ShortComplex C} (Ï† : Sâ‚ âŸ¶ Sâ‚‚)
    (hâ‚ : Sâ‚.Exact) [Mono Sâ‚.f] [Mono Sâ‚‚.f] [Mono Ï†.Ï„â‚] [Mono Ï†.Ï„â‚ƒ] : Mono Ï†.Ï„â‚‚ := by
  rw [mono_iff_cancel_zero]
  intro A xâ‚‚ hxâ‚‚
  obtain âŸ¨xâ‚, hxâ‚âŸ© : âˆƒ xâ‚, xâ‚ â‰« Sâ‚.f = xâ‚‚ := âŸ¨_, hâ‚.lift_f xâ‚‚
    (by simp only [â† cancel_mono Ï†.Ï„â‚ƒ, assoc, zero_comp, â† Ï†.commâ‚‚â‚ƒ, reassoc_of% hxâ‚‚])âŸ©
  suffices xâ‚ = 0 by rw [â† hxâ‚, this, zero_comp]
  simp only [â† cancel_mono Ï†.Ï„â‚, â† cancel_mono Sâ‚‚.f, assoc, Ï†.commâ‚â‚‚, zero_comp,
    reassoc_of% hxâ‚, hxâ‚‚]

lemma mono_Ï„â‚‚_of_exact_of_mono' {Sâ‚ Sâ‚‚ : ShortComplex C} (Ï† : Sâ‚ âŸ¶ Sâ‚‚)
    (hâ‚ : Sâ‚.Exact) (hfâ‚ : Mono Sâ‚.f) (hfâ‚‚ : Mono Sâ‚‚.f) (hÏ„â‚ : Mono Ï†.Ï„â‚) (hÏ„â‚‚ : Mono Ï†.Ï„â‚ƒ) :
    Mono Ï†.Ï„â‚‚ := by
  apply mono_Ï„â‚‚_of_exact_of_mono Ï† hâ‚

attribute [local instance] balanced_opposite

lemma epi_Ï„â‚‚_of_exact_of_epi {Sâ‚ Sâ‚‚ : ShortComplex C} (Ï† : Sâ‚ âŸ¶ Sâ‚‚)
    (hâ‚‚ : Sâ‚‚.Exact) [Epi Sâ‚.g] [Epi Sâ‚‚.g] [Epi Ï†.Ï„â‚] [Epi Ï†.Ï„â‚ƒ] : Epi Ï†.Ï„â‚‚ := by
  have := mono_Ï„â‚‚_of_exact_of_mono' (opMap Ï†) hâ‚‚.op (op_mono_of_epi Sâ‚‚.g)
    (op_mono_of_epi Sâ‚.g) (op_mono_of_epi Ï†.Ï„â‚ƒ) (op_mono_of_epi Ï†.Ï„â‚)
  exact unop_epi_of_mono (opMap Ï†).Ï„â‚‚

end Balanced

end Preadditive

end ShortComplex

end CategoryTheory
