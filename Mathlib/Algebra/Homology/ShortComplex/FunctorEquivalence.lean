/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Homology.ShortComplex.Basic

/-!
# Short complexes in functor categories

In this file, it is shown that if `J` and `C` are two categories (such
that `C` has zero morphisms), then there is an equivalence of categories
`ShortComplex.functorEquivalence J C : ShortComplex (J ‚•§ C) ‚âå J ‚•§ ShortComplex C`.

-/

namespace CategoryTheory

open Limits Functor

variable (J C : Type*) [Category J] [Category C] [HasZeroMorphisms C]

namespace ShortComplex

namespace FunctorEquivalence

attribute [local simp] ShortComplex.Hom.comm‚ÇÅ‚ÇÇ ShortComplex.Hom.comm‚ÇÇ‚ÇÉ

/-- The obvious functor `ShortComplex (J ‚•§ C) ‚•§ J ‚•§ ShortComplex C`. -/
@[simps]
def functor : ShortComplex (J ‚•§ C) ‚•§ J ‚•§ ShortComplex C where
  obj S :=
    { obj := fun j ‚Ü¶ S.map ((evaluation J C).obj j)
      map := fun f ‚Ü¶ S.mapNatTrans ((evaluation J C).map f) }
  map œÜ :=
    { app := fun j ‚Ü¶ ((evaluation J C).obj j).mapShortComplex.map œÜ }

/-- The obvious functor `(J ‚•§ ShortComplex C) ‚•§ ShortComplex (J ‚•§ C)`. -/
@[simps]
def inverse : (J ‚•§ ShortComplex C) ‚•§ ShortComplex (J ‚•§ C) where
  obj F :=
    { f := whiskerLeft F œÄ‚ÇÅToœÄ‚ÇÇ
      g := whiskerLeft F œÄ‚ÇÇToœÄ‚ÇÉ
      zero := by cat_disch }
  map œÜ := Hom.mk (whiskerRight œÜ œÄ‚ÇÅ) (whiskerRight œÜ œÄ‚ÇÇ) (whiskerRight œÜ œÄ‚ÇÉ)
    (by cat_disch) (by cat_disch)

/-- The unit isomorphism of the equivalence
`ShortComplex.functorEquivalence : ShortComplex (J ‚•§ C) ‚âå J ‚•§ ShortComplex C`. -/
@[simps!]
def unitIso : ùü≠ _ ‚âÖ functor J C ‚ãô inverse J C :=
  NatIso.ofComponents (fun _ ‚Ü¶ isoMk
    (NatIso.ofComponents (fun _ ‚Ü¶ Iso.refl _) (by simp))
    (NatIso.ofComponents (fun _ ‚Ü¶ Iso.refl _) (by simp))
    (NatIso.ofComponents (fun _ ‚Ü¶ Iso.refl _) (by simp))
    (by cat_disch) (by cat_disch)) (by cat_disch)

/-- The counit isomorphism of the equivalence
`ShortComplex.functorEquivalence : ShortComplex (J ‚•§ C) ‚âå J ‚•§ ShortComplex C`. -/
@[simps!]
def counitIso : inverse J C ‚ãô functor J C ‚âÖ ùü≠ _ :=
  NatIso.ofComponents (fun _ ‚Ü¶ NatIso.ofComponents
    (fun _ ‚Ü¶ isoMk (Iso.refl _) (Iso.refl _) (Iso.refl _)
      (by simp) (by simp)) (by cat_disch)) (by cat_disch)

end FunctorEquivalence

/-- The obvious equivalence `ShortComplex (J ‚•§ C) ‚âå J ‚•§ ShortComplex C`. -/
@[simps]
def functorEquivalence : ShortComplex (J ‚•§ C) ‚âå J ‚•§ ShortComplex C where
  functor := FunctorEquivalence.functor J C
  inverse := FunctorEquivalence.inverse J C
  unitIso := FunctorEquivalence.unitIso J C
  counitIso := FunctorEquivalence.counitIso J C

end ShortComplex

end CategoryTheory
