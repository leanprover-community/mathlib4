/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Homology.ShortComplex.Homology
import Mathlib.CategoryTheory.Preadditive.AdditiveFunctor
import Mathlib.CategoryTheory.Preadditive.Opposite

/-!
# Homology of preadditive categories

In this file, it is shown that if `C` is a preadditive category, then
`ShortComplex C` is a preadditive category.

-/

namespace CategoryTheory

open Category Limits Preadditive

variable {C : Type*} [Category C] [Preadditive C]

namespace ShortComplex

variable {S‚ÇÅ S‚ÇÇ S‚ÇÉ : ShortComplex C}

attribute [local simp] Hom.comm‚ÇÅ‚ÇÇ Hom.comm‚ÇÇ‚ÇÉ

instance : Add (S‚ÇÅ ‚ü∂ S‚ÇÇ) where
  add œÜ œÜ' :=
    { œÑ‚ÇÅ := œÜ.œÑ‚ÇÅ + œÜ'.œÑ‚ÇÅ
      œÑ‚ÇÇ := œÜ.œÑ‚ÇÇ + œÜ'.œÑ‚ÇÇ
      œÑ‚ÇÉ := œÜ.œÑ‚ÇÉ + œÜ'.œÑ‚ÇÉ }

instance : Sub (S‚ÇÅ ‚ü∂ S‚ÇÇ) where
  sub œÜ œÜ' :=
    { œÑ‚ÇÅ := œÜ.œÑ‚ÇÅ - œÜ'.œÑ‚ÇÅ
      œÑ‚ÇÇ := œÜ.œÑ‚ÇÇ - œÜ'.œÑ‚ÇÇ
      œÑ‚ÇÉ := œÜ.œÑ‚ÇÉ - œÜ'.œÑ‚ÇÉ }

instance : Neg (S‚ÇÅ ‚ü∂ S‚ÇÇ) where
  neg œÜ :=
    { œÑ‚ÇÅ := -œÜ.œÑ‚ÇÅ
      œÑ‚ÇÇ := -œÜ.œÑ‚ÇÇ
      œÑ‚ÇÉ := -œÜ.œÑ‚ÇÉ }

instance : AddCommGroup (S‚ÇÅ ‚ü∂ S‚ÇÇ) where
  add_assoc := fun a b c => by ext <;> apply add_assoc
  add_zero := fun a => by ext <;> apply add_zero
  zero_add := fun a => by ext <;> apply zero_add
  add_left_neg := fun a => by ext <;> apply add_left_neg
  add_comm := fun a b => by ext <;> apply add_comm
  sub_eq_add_neg := fun a b => by ext <;> apply sub_eq_add_neg
  nsmul := nsmulRec
  zsmul := zsmulRec

@[simp] lemma add_œÑ‚ÇÅ (œÜ œÜ' : S‚ÇÅ ‚ü∂ S‚ÇÇ) : (œÜ + œÜ').œÑ‚ÇÅ = œÜ.œÑ‚ÇÅ + œÜ'.œÑ‚ÇÅ := rfl
@[simp] lemma add_œÑ‚ÇÇ (œÜ œÜ' : S‚ÇÅ ‚ü∂ S‚ÇÇ) : (œÜ + œÜ').œÑ‚ÇÇ = œÜ.œÑ‚ÇÇ + œÜ'.œÑ‚ÇÇ := rfl
@[simp] lemma add_œÑ‚ÇÉ (œÜ œÜ' : S‚ÇÅ ‚ü∂ S‚ÇÇ) : (œÜ + œÜ').œÑ‚ÇÉ = œÜ.œÑ‚ÇÉ + œÜ'.œÑ‚ÇÉ := rfl
@[simp] lemma sub_œÑ‚ÇÅ (œÜ œÜ' : S‚ÇÅ ‚ü∂ S‚ÇÇ) : (œÜ - œÜ').œÑ‚ÇÅ = œÜ.œÑ‚ÇÅ - œÜ'.œÑ‚ÇÅ := rfl
@[simp] lemma sub_œÑ‚ÇÇ (œÜ œÜ' : S‚ÇÅ ‚ü∂ S‚ÇÇ) : (œÜ - œÜ').œÑ‚ÇÇ = œÜ.œÑ‚ÇÇ - œÜ'.œÑ‚ÇÇ := rfl
@[simp] lemma sub_œÑ‚ÇÉ (œÜ œÜ' : S‚ÇÅ ‚ü∂ S‚ÇÇ) : (œÜ - œÜ').œÑ‚ÇÉ = œÜ.œÑ‚ÇÉ - œÜ'.œÑ‚ÇÉ := rfl
@[simp] lemma neg_œÑ‚ÇÅ (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) : (-œÜ).œÑ‚ÇÅ = -œÜ.œÑ‚ÇÅ := rfl
@[simp] lemma neg_œÑ‚ÇÇ (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) : (-œÜ).œÑ‚ÇÇ = -œÜ.œÑ‚ÇÇ := rfl
@[simp] lemma neg_œÑ‚ÇÉ (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) : (-œÜ).œÑ‚ÇÉ = -œÜ.œÑ‚ÇÉ := rfl

instance : Preadditive (ShortComplex C) where

section LeftHomology

variable {œÜ œÜ' : S‚ÇÅ ‚ü∂ S‚ÇÇ} {h‚ÇÅ : S‚ÇÅ.LeftHomologyData} {h‚ÇÇ : S‚ÇÇ.LeftHomologyData}

namespace LeftHomologyMapData

variable (Œ≥ : LeftHomologyMapData œÜ h‚ÇÅ h‚ÇÇ) (Œ≥' : LeftHomologyMapData œÜ' h‚ÇÅ h‚ÇÇ)

/-- Given a left homology map data for morphism `œÜ`, this is the induced left homology
map data for `-œÜ`. -/
@[simps]
def neg : LeftHomologyMapData (-œÜ) h‚ÇÅ h‚ÇÇ where
  œÜK := -Œ≥.œÜK
  œÜH := -Œ≥.œÜH

/-- Given left homology map data for morphisms `œÜ` and `œÜ'`, this is
the induced left homology map data for `œÜ + œÜ'`. -/
@[simps]
def add : LeftHomologyMapData (œÜ + œÜ') h‚ÇÅ h‚ÇÇ where
  œÜK := Œ≥.œÜK + Œ≥'.œÜK
  œÜH := Œ≥.œÜH + Œ≥'.œÜH

end LeftHomologyMapData

variable (h‚ÇÅ h‚ÇÇ)

@[simp]
lemma leftHomologyMap'_neg :
    leftHomologyMap' (-œÜ) h‚ÇÅ h‚ÇÇ = -leftHomologyMap' œÜ h‚ÇÅ h‚ÇÇ := by
  have Œ≥ : LeftHomologyMapData œÜ h‚ÇÅ h‚ÇÇ := default
  simp only [Œ≥.leftHomologyMap'_eq, Œ≥.neg.leftHomologyMap'_eq, LeftHomologyMapData.neg_œÜH]

@[simp]
lemma cyclesMap'_neg :
    cyclesMap' (-œÜ) h‚ÇÅ h‚ÇÇ = -cyclesMap' œÜ h‚ÇÅ h‚ÇÇ := by
  have Œ≥ : LeftHomologyMapData œÜ h‚ÇÅ h‚ÇÇ := default
  simp only [Œ≥.cyclesMap'_eq, Œ≥.neg.cyclesMap'_eq, LeftHomologyMapData.neg_œÜK]

@[simp]
lemma leftHomologyMap'_add :
    leftHomologyMap' (œÜ + œÜ') h‚ÇÅ h‚ÇÇ = leftHomologyMap' œÜ h‚ÇÅ h‚ÇÇ +
      leftHomologyMap' œÜ' h‚ÇÅ h‚ÇÇ := by
  have Œ≥ : LeftHomologyMapData œÜ h‚ÇÅ h‚ÇÇ := default
  have Œ≥' : LeftHomologyMapData œÜ' h‚ÇÅ h‚ÇÇ := default
  simp only [Œ≥.leftHomologyMap'_eq, Œ≥'.leftHomologyMap'_eq,
    (Œ≥.add Œ≥').leftHomologyMap'_eq, LeftHomologyMapData.add_œÜH]

@[simp]
lemma cyclesMap'_add :
    cyclesMap' (œÜ + œÜ') h‚ÇÅ h‚ÇÇ = cyclesMap' œÜ h‚ÇÅ h‚ÇÇ +
      cyclesMap' œÜ' h‚ÇÅ h‚ÇÇ := by
  have Œ≥ : LeftHomologyMapData œÜ h‚ÇÅ h‚ÇÇ := default
  have Œ≥' : LeftHomologyMapData œÜ' h‚ÇÅ h‚ÇÇ := default
  simp only [Œ≥.cyclesMap'_eq, Œ≥'.cyclesMap'_eq,
    (Œ≥.add Œ≥').cyclesMap'_eq, LeftHomologyMapData.add_œÜK]

@[simp]
lemma leftHomologyMap'_sub :
    leftHomologyMap' (œÜ - œÜ') h‚ÇÅ h‚ÇÇ = leftHomologyMap' œÜ h‚ÇÅ h‚ÇÇ -
      leftHomologyMap' œÜ' h‚ÇÅ h‚ÇÇ := by
  simp only [sub_eq_add_neg, leftHomologyMap'_add, leftHomologyMap'_neg]

@[simp]
lemma cyclesMap'_sub :
    cyclesMap' (œÜ - œÜ') h‚ÇÅ h‚ÇÇ = cyclesMap' œÜ h‚ÇÅ h‚ÇÇ -
      cyclesMap' œÜ' h‚ÇÅ h‚ÇÇ := by
  simp only [sub_eq_add_neg, cyclesMap'_add, cyclesMap'_neg]

variable (œÜ œÜ')

section

variable [S‚ÇÅ.HasLeftHomology] [S‚ÇÇ.HasLeftHomology]

@[simp]
lemma leftHomologyMap_neg : leftHomologyMap (-œÜ) = -leftHomologyMap œÜ :=
  leftHomologyMap'_neg _ _

@[simp]
lemma cyclesMap_neg : cyclesMap (-œÜ) = -cyclesMap œÜ :=
  cyclesMap'_neg _ _

@[simp]
lemma leftHomologyMap_add : leftHomologyMap (œÜ + œÜ') = leftHomologyMap œÜ + leftHomologyMap œÜ' :=
  leftHomologyMap'_add _ _

@[simp]
lemma cyclesMap_add : cyclesMap (œÜ + œÜ') = cyclesMap œÜ + cyclesMap œÜ' :=
  cyclesMap'_add _ _

@[simp]
lemma leftHomologyMap_sub : leftHomologyMap (œÜ - œÜ') = leftHomologyMap œÜ - leftHomologyMap œÜ' :=
  leftHomologyMap'_sub _ _

@[simp]
lemma cyclesMap_sub : cyclesMap (œÜ - œÜ') = cyclesMap œÜ - cyclesMap œÜ' :=
  cyclesMap'_sub _ _

end

instance leftHomologyFunctor_additive [HasKernels C] [HasCokernels C] :
  (leftHomologyFunctor C).Additive where

instance cyclesFunctor_additive [HasKernels C] [HasCokernels C] :
  (cyclesFunctor C).Additive where

end LeftHomology


section RightHomology

variable {œÜ œÜ' : S‚ÇÅ ‚ü∂ S‚ÇÇ} {h‚ÇÅ : S‚ÇÅ.RightHomologyData} {h‚ÇÇ : S‚ÇÇ.RightHomologyData}

namespace RightHomologyMapData

variable (Œ≥ : RightHomologyMapData œÜ h‚ÇÅ h‚ÇÇ) (Œ≥' : RightHomologyMapData œÜ' h‚ÇÅ h‚ÇÇ)

/-- Given a right homology map data for morphism `œÜ`, this is the induced right homology
map data for `-œÜ`. -/
@[simps]
def neg : RightHomologyMapData (-œÜ) h‚ÇÅ h‚ÇÇ where
  œÜQ := -Œ≥.œÜQ
  œÜH := -Œ≥.œÜH

/-- Given right homology map data for morphisms `œÜ` and `œÜ'`, this is the induced
right homology map data for `œÜ + œÜ'`. -/
@[simps]
def add : RightHomologyMapData (œÜ + œÜ') h‚ÇÅ h‚ÇÇ where
  œÜQ := Œ≥.œÜQ + Œ≥'.œÜQ
  œÜH := Œ≥.œÜH + Œ≥'.œÜH

end RightHomologyMapData

variable (h‚ÇÅ h‚ÇÇ)

@[simp]
lemma rightHomologyMap'_neg :
    rightHomologyMap' (-œÜ) h‚ÇÅ h‚ÇÇ = -rightHomologyMap' œÜ h‚ÇÅ h‚ÇÇ := by
  have Œ≥ : RightHomologyMapData œÜ h‚ÇÅ h‚ÇÇ := default
  simp only [Œ≥.rightHomologyMap'_eq, Œ≥.neg.rightHomologyMap'_eq, RightHomologyMapData.neg_œÜH]

@[simp]
lemma opcyclesMap'_neg :
    opcyclesMap' (-œÜ) h‚ÇÅ h‚ÇÇ = -opcyclesMap' œÜ h‚ÇÅ h‚ÇÇ := by
  have Œ≥ : RightHomologyMapData œÜ h‚ÇÅ h‚ÇÇ := default
  simp only [Œ≥.opcyclesMap'_eq, Œ≥.neg.opcyclesMap'_eq, RightHomologyMapData.neg_œÜQ]

@[simp]
lemma rightHomologyMap'_add :
    rightHomologyMap' (œÜ + œÜ') h‚ÇÅ h‚ÇÇ = rightHomologyMap' œÜ h‚ÇÅ h‚ÇÇ +
      rightHomologyMap' œÜ' h‚ÇÅ h‚ÇÇ := by
  have Œ≥ : RightHomologyMapData œÜ h‚ÇÅ h‚ÇÇ := default
  have Œ≥' : RightHomologyMapData œÜ' h‚ÇÅ h‚ÇÇ := default
  simp only [Œ≥.rightHomologyMap'_eq, Œ≥'.rightHomologyMap'_eq,
    (Œ≥.add Œ≥').rightHomologyMap'_eq, RightHomologyMapData.add_œÜH]

@[simp]
lemma opcyclesMap'_add :
    opcyclesMap' (œÜ + œÜ') h‚ÇÅ h‚ÇÇ = opcyclesMap' œÜ h‚ÇÅ h‚ÇÇ +
      opcyclesMap' œÜ' h‚ÇÅ h‚ÇÇ := by
  have Œ≥ : RightHomologyMapData œÜ h‚ÇÅ h‚ÇÇ := default
  have Œ≥' : RightHomologyMapData œÜ' h‚ÇÅ h‚ÇÇ := default
  simp only [Œ≥.opcyclesMap'_eq, Œ≥'.opcyclesMap'_eq,
    (Œ≥.add Œ≥').opcyclesMap'_eq, RightHomologyMapData.add_œÜQ]

@[simp]
lemma rightHomologyMap'_sub :
    rightHomologyMap' (œÜ - œÜ') h‚ÇÅ h‚ÇÇ = rightHomologyMap' œÜ h‚ÇÅ h‚ÇÇ -
      rightHomologyMap' œÜ' h‚ÇÅ h‚ÇÇ := by
  simp only [sub_eq_add_neg, rightHomologyMap'_add, rightHomologyMap'_neg]

@[simp]
lemma opcyclesMap'_sub :
    opcyclesMap' (œÜ - œÜ') h‚ÇÅ h‚ÇÇ = opcyclesMap' œÜ h‚ÇÅ h‚ÇÇ -
      opcyclesMap' œÜ' h‚ÇÅ h‚ÇÇ := by
  simp only [sub_eq_add_neg, opcyclesMap'_add, opcyclesMap'_neg]

variable (œÜ œÜ')

section

variable [S‚ÇÅ.HasRightHomology] [S‚ÇÇ.HasRightHomology]

@[simp]
lemma rightHomologyMap_neg : rightHomologyMap (-œÜ) = -rightHomologyMap œÜ :=
  rightHomologyMap'_neg _ _

@[simp]
lemma opcyclesMap_neg : opcyclesMap (-œÜ) = -opcyclesMap œÜ :=
  opcyclesMap'_neg _ _

@[simp]
lemma rightHomologyMap_add :
    rightHomologyMap (œÜ + œÜ') = rightHomologyMap œÜ + rightHomologyMap œÜ' :=
  rightHomologyMap'_add _ _

@[simp]
lemma opcyclesMap_add : opcyclesMap (œÜ + œÜ') = opcyclesMap œÜ + opcyclesMap œÜ' :=
  opcyclesMap'_add _ _

@[simp]
lemma rightHomologyMap_sub :
    rightHomologyMap (œÜ - œÜ') = rightHomologyMap œÜ - rightHomologyMap œÜ' :=
  rightHomologyMap'_sub _ _

@[simp]
lemma opcyclesMap_sub : opcyclesMap (œÜ - œÜ') = opcyclesMap œÜ - opcyclesMap œÜ' :=
  opcyclesMap'_sub _ _

end

instance rightHomologyFunctor_additive [HasKernels C] [HasCokernels C] :
  (rightHomologyFunctor C).Additive where

instance opcyclesFunctor_additive [HasKernels C] [HasCokernels C] :
  (opcyclesFunctor C).Additive where

end RightHomology

section Homology

variable {œÜ œÜ' : S‚ÇÅ ‚ü∂ S‚ÇÇ} {h‚ÇÅ : S‚ÇÅ.HomologyData} {h‚ÇÇ : S‚ÇÇ.HomologyData}

namespace HomologyMapData

variable (Œ≥ : HomologyMapData œÜ h‚ÇÅ h‚ÇÇ) (Œ≥' : HomologyMapData œÜ' h‚ÇÅ h‚ÇÇ)

/-- Given a homology map data for a morphism `œÜ`, this is the induced homology
map data for `-œÜ`. -/
@[simps]
def neg : HomologyMapData (-œÜ) h‚ÇÅ h‚ÇÇ where
  left := Œ≥.left.neg
  right := Œ≥.right.neg

/-- Given homology map data for morphisms `œÜ` and `œÜ'`, this is the induced homology
map data for `œÜ + œÜ'`. -/
@[simps]
def add : HomologyMapData (œÜ + œÜ') h‚ÇÅ h‚ÇÇ where
  left := Œ≥.left.add Œ≥'.left
  right := Œ≥.right.add Œ≥'.right

end HomologyMapData

variable (h‚ÇÅ h‚ÇÇ)

@[simp]
lemma homologyMap'_neg :
    homologyMap' (-œÜ) h‚ÇÅ h‚ÇÇ = -homologyMap' œÜ h‚ÇÅ h‚ÇÇ :=
  leftHomologyMap'_neg _ _

@[simp]
lemma homologyMap'_add :
    homologyMap' (œÜ + œÜ') h‚ÇÅ h‚ÇÇ = homologyMap' œÜ h‚ÇÅ h‚ÇÇ + homologyMap' œÜ' h‚ÇÅ h‚ÇÇ :=
  leftHomologyMap'_add _ _

@[simp]
lemma homologyMap'_sub :
    homologyMap' (œÜ - œÜ') h‚ÇÅ h‚ÇÇ = homologyMap' œÜ h‚ÇÅ h‚ÇÇ - homologyMap' œÜ' h‚ÇÅ h‚ÇÇ :=
  leftHomologyMap'_sub _ _

variable (œÜ œÜ')

section

variable [S‚ÇÅ.HasHomology] [S‚ÇÇ.HasHomology]

@[simp]
lemma homologyMap_neg : homologyMap (-œÜ) = -homologyMap œÜ :=
  homologyMap'_neg _ _

@[simp]
lemma homologyMap_add : homologyMap (œÜ + œÜ') = homologyMap œÜ + homologyMap œÜ' :=
  homologyMap'_add _ _

@[simp]
lemma homologyMap_sub : homologyMap (œÜ - œÜ') = homologyMap œÜ - homologyMap œÜ' :=
  homologyMap'_sub _ _

end

instance homologyFunctor_additive [CategoryWithHomology C] :
  (homologyFunctor C).Additive where

end Homology

section Homotopy

variable (œÜ‚ÇÅ œÜ‚ÇÇ œÜ‚ÇÉ œÜ‚ÇÑ : S‚ÇÅ ‚ü∂ S‚ÇÇ)

/-- A homotopy between two morphisms of short complexes `S‚ÇÅ ‚ü∂ S‚ÇÇ` consists of various
maps and conditions which will be sufficient to show that they induce the same morphism
in homology. -/
@[ext]
structure Homotopy where
  /-- a morphism `S‚ÇÅ.X‚ÇÅ ‚ü∂ S‚ÇÇ.X‚ÇÅ` -/
  h‚ÇÄ : S‚ÇÅ.X‚ÇÅ ‚ü∂ S‚ÇÇ.X‚ÇÅ
  h‚ÇÄ_f : h‚ÇÄ ‚â´ S‚ÇÇ.f = 0 := by aesop_cat
  /-- a morphism `S‚ÇÅ.X‚ÇÇ ‚ü∂ S‚ÇÇ.X‚ÇÅ` -/
  h‚ÇÅ : S‚ÇÅ.X‚ÇÇ ‚ü∂ S‚ÇÇ.X‚ÇÅ
  /-- a morphism `S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÇ` -/
  h‚ÇÇ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÇ
  /-- a morphism `S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÉ` -/
  h‚ÇÉ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÉ
  g_h‚ÇÉ : S‚ÇÅ.g ‚â´ h‚ÇÉ = 0 := by aesop_cat
  comm‚ÇÅ : œÜ‚ÇÅ.œÑ‚ÇÅ = S‚ÇÅ.f ‚â´ h‚ÇÅ + h‚ÇÄ + œÜ‚ÇÇ.œÑ‚ÇÅ := by aesop_cat
  comm‚ÇÇ : œÜ‚ÇÅ.œÑ‚ÇÇ = S‚ÇÅ.g ‚â´ h‚ÇÇ + h‚ÇÅ ‚â´ S‚ÇÇ.f + œÜ‚ÇÇ.œÑ‚ÇÇ := by aesop_cat
  comm‚ÇÉ : œÜ‚ÇÅ.œÑ‚ÇÉ = h‚ÇÉ + h‚ÇÇ ‚â´ S‚ÇÇ.g + œÜ‚ÇÇ.œÑ‚ÇÉ := by aesop_cat

attribute [reassoc (attr := simp)] Homotopy.h‚ÇÄ_f Homotopy.g_h‚ÇÉ

variable (S‚ÇÅ S‚ÇÇ)

/-- Constructor for null homotopic morphisms, see also `Homotopy.ofNullHomotopic`
and `Homotopy.eq_add_nullHomotopic`. -/
@[simps]
def nullHomotopic (h‚ÇÄ : S‚ÇÅ.X‚ÇÅ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÄ_f : h‚ÇÄ ‚â´ S‚ÇÇ.f = 0)
    (h‚ÇÅ : S‚ÇÅ.X‚ÇÇ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÇ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÇ) (h‚ÇÉ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÉ) (g_h‚ÇÉ : S‚ÇÅ.g ‚â´ h‚ÇÉ = 0) :
    S‚ÇÅ ‚ü∂ S‚ÇÇ where
  œÑ‚ÇÅ := h‚ÇÄ + S‚ÇÅ.f ‚â´ h‚ÇÅ
  œÑ‚ÇÇ := h‚ÇÅ ‚â´ S‚ÇÇ.f + S‚ÇÅ.g ‚â´ h‚ÇÇ
  œÑ‚ÇÉ := h‚ÇÇ ‚â´ S‚ÇÇ.g + h‚ÇÉ

namespace Homotopy

attribute [local simp] neg_comp

variable {S‚ÇÅ S‚ÇÇ œÜ‚ÇÅ œÜ‚ÇÇ œÜ‚ÇÉ œÜ‚ÇÑ}

/-- The obvious homotopy between two equal morphisms of short complexes. -/
@[simps]
def ofEq (h : œÜ‚ÇÅ = œÜ‚ÇÇ) : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ where
  h‚ÇÄ := 0
  h‚ÇÅ := 0
  h‚ÇÇ := 0
  h‚ÇÉ := 0

/-- The obvious homotopy between a morphism of short complexes and itself. -/
@[simps!]
def refl (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) : Homotopy œÜ œÜ := ofEq rfl

/-- The symmetry of homotopy between morphisms of short complexes. -/
@[simps]
def symm (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) : Homotopy œÜ‚ÇÇ œÜ‚ÇÅ where
  h‚ÇÄ := -h.h‚ÇÄ
  h‚ÇÅ := -h.h‚ÇÅ
  h‚ÇÇ := -h.h‚ÇÇ
  h‚ÇÉ := -h.h‚ÇÉ
  comm‚ÇÅ := by rw [h.comm‚ÇÅ, comp_neg]; abel
  comm‚ÇÇ := by rw [h.comm‚ÇÇ, comp_neg, neg_comp]; abel
  comm‚ÇÉ := by rw [h.comm‚ÇÉ, neg_comp]; abel

/-- If two maps of short complexes are homotopic, their opposites also are. -/
@[simps]
def neg (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) : Homotopy (-œÜ‚ÇÅ) (-œÜ‚ÇÇ) where
  h‚ÇÄ := -h.h‚ÇÄ
  h‚ÇÅ := -h.h‚ÇÅ
  h‚ÇÇ := -h.h‚ÇÇ
  h‚ÇÉ := -h.h‚ÇÉ
  comm‚ÇÅ := by rw [neg_œÑ‚ÇÅ, neg_œÑ‚ÇÅ, h.comm‚ÇÅ, neg_add_rev, comp_neg]; abel
  comm‚ÇÇ := by rw [neg_œÑ‚ÇÇ, neg_œÑ‚ÇÇ, h.comm‚ÇÇ, neg_add_rev, comp_neg, neg_comp]; abel
  comm‚ÇÉ := by rw [neg_œÑ‚ÇÉ, neg_œÑ‚ÇÉ, h.comm‚ÇÉ, neg_comp]; abel

/-- The transitivity of homotopy between morphisms of short complexes. -/
@[simps]
def trans (h‚ÇÅ‚ÇÇ : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) (h‚ÇÇ‚ÇÉ : Homotopy œÜ‚ÇÇ œÜ‚ÇÉ) : Homotopy œÜ‚ÇÅ œÜ‚ÇÉ where
  h‚ÇÄ := h‚ÇÅ‚ÇÇ.h‚ÇÄ + h‚ÇÇ‚ÇÉ.h‚ÇÄ
  h‚ÇÅ := h‚ÇÅ‚ÇÇ.h‚ÇÅ + h‚ÇÇ‚ÇÉ.h‚ÇÅ
  h‚ÇÇ := h‚ÇÅ‚ÇÇ.h‚ÇÇ + h‚ÇÇ‚ÇÉ.h‚ÇÇ
  h‚ÇÉ := h‚ÇÅ‚ÇÇ.h‚ÇÉ + h‚ÇÇ‚ÇÉ.h‚ÇÉ
  comm‚ÇÅ := by rw [h‚ÇÅ‚ÇÇ.comm‚ÇÅ, h‚ÇÇ‚ÇÉ.comm‚ÇÅ, comp_add]; abel
  comm‚ÇÇ := by rw [h‚ÇÅ‚ÇÇ.comm‚ÇÇ, h‚ÇÇ‚ÇÉ.comm‚ÇÇ, comp_add, add_comp]; abel
  comm‚ÇÉ := by rw [h‚ÇÅ‚ÇÇ.comm‚ÇÉ, h‚ÇÇ‚ÇÉ.comm‚ÇÉ, add_comp]; abel

/-- Homotopy between morphisms of short complexes is compatible with addition. -/
@[simps]
def add (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) (h' : Homotopy œÜ‚ÇÉ œÜ‚ÇÑ) : Homotopy (œÜ‚ÇÅ + œÜ‚ÇÉ) (œÜ‚ÇÇ + œÜ‚ÇÑ) where
  h‚ÇÄ := h.h‚ÇÄ + h'.h‚ÇÄ
  h‚ÇÅ := h.h‚ÇÅ + h'.h‚ÇÅ
  h‚ÇÇ := h.h‚ÇÇ + h'.h‚ÇÇ
  h‚ÇÉ := h.h‚ÇÉ + h'.h‚ÇÉ
  comm‚ÇÅ := by rw [add_œÑ‚ÇÅ, add_œÑ‚ÇÅ, h.comm‚ÇÅ, h'.comm‚ÇÅ, comp_add]; abel
  comm‚ÇÇ := by rw [add_œÑ‚ÇÇ, add_œÑ‚ÇÇ, h.comm‚ÇÇ, h'.comm‚ÇÇ, comp_add, add_comp]; abel
  comm‚ÇÉ := by rw [add_œÑ‚ÇÉ, add_œÑ‚ÇÉ, h.comm‚ÇÉ, h'.comm‚ÇÉ, add_comp]; abel

/-- Homotopy between morphisms of short complexes is compatible with substraction. -/
@[simps]
def sub (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) (h' : Homotopy œÜ‚ÇÉ œÜ‚ÇÑ) : Homotopy (œÜ‚ÇÅ - œÜ‚ÇÉ) (œÜ‚ÇÇ - œÜ‚ÇÑ) where
  h‚ÇÄ := h.h‚ÇÄ - h'.h‚ÇÄ
  h‚ÇÅ := h.h‚ÇÅ - h'.h‚ÇÅ
  h‚ÇÇ := h.h‚ÇÇ - h'.h‚ÇÇ
  h‚ÇÉ := h.h‚ÇÉ - h'.h‚ÇÉ
  comm‚ÇÅ := by rw [sub_œÑ‚ÇÅ, sub_œÑ‚ÇÅ, h.comm‚ÇÅ, h'.comm‚ÇÅ, comp_sub]; abel
  comm‚ÇÇ := by rw [sub_œÑ‚ÇÇ, sub_œÑ‚ÇÇ, h.comm‚ÇÇ, h'.comm‚ÇÇ, comp_sub, sub_comp]; abel
  comm‚ÇÉ := by rw [sub_œÑ‚ÇÉ, sub_œÑ‚ÇÉ, h.comm‚ÇÉ, h'.comm‚ÇÉ, sub_comp]; abel

/-- Homotopy between morphisms of short complexes is compatible with precomposition. -/
@[simps]
def compLeft (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) (œà : S‚ÇÉ ‚ü∂ S‚ÇÅ) : Homotopy (œà ‚â´ œÜ‚ÇÅ) (œà ‚â´ œÜ‚ÇÇ) where
  h‚ÇÄ := œà.œÑ‚ÇÅ ‚â´ h.h‚ÇÄ
  h‚ÇÅ := œà.œÑ‚ÇÇ ‚â´ h.h‚ÇÅ
  h‚ÇÇ := œà.œÑ‚ÇÉ ‚â´ h.h‚ÇÇ
  h‚ÇÉ := œà.œÑ‚ÇÉ ‚â´ h.h‚ÇÉ
  g_h‚ÇÉ := by rw [‚Üê œà.comm‚ÇÇ‚ÇÉ_assoc, h.g_h‚ÇÉ, comp_zero]
  comm‚ÇÅ := by rw [comp_œÑ‚ÇÅ, comp_œÑ‚ÇÅ, h.comm‚ÇÅ, comp_add, comp_add, add_left_inj, œà.comm‚ÇÅ‚ÇÇ_assoc]
  comm‚ÇÇ := by rw [comp_œÑ‚ÇÇ, comp_œÑ‚ÇÇ, h.comm‚ÇÇ, comp_add, comp_add, assoc, œà.comm‚ÇÇ‚ÇÉ_assoc]
  comm‚ÇÉ := by rw [comp_œÑ‚ÇÉ, comp_œÑ‚ÇÉ, h.comm‚ÇÉ, comp_add, comp_add, assoc]

/-- Homotopy between morphisms of short complexes is compatible with postcomposition. -/
@[simps]
def compRight (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) (œà : S‚ÇÇ ‚ü∂ S‚ÇÉ) : Homotopy (œÜ‚ÇÅ ‚â´ œà) (œÜ‚ÇÇ ‚â´ œà) where
  h‚ÇÄ := h.h‚ÇÄ ‚â´ œà.œÑ‚ÇÅ
  h‚ÇÅ := h.h‚ÇÅ ‚â´ œà.œÑ‚ÇÅ
  h‚ÇÇ := h.h‚ÇÇ ‚â´ œà.œÑ‚ÇÇ
  h‚ÇÉ := h.h‚ÇÉ ‚â´ œà.œÑ‚ÇÉ
  comm‚ÇÅ := by rw [comp_œÑ‚ÇÅ, comp_œÑ‚ÇÅ, h.comm‚ÇÅ, add_comp, add_comp, assoc]
  comm‚ÇÇ := by rw [comp_œÑ‚ÇÇ, comp_œÑ‚ÇÇ, h.comm‚ÇÇ, add_comp, add_comp, assoc, assoc, assoc, œà.comm‚ÇÅ‚ÇÇ]
  comm‚ÇÉ := by rw [comp_œÑ‚ÇÉ, comp_œÑ‚ÇÉ, h.comm‚ÇÉ, add_comp, add_comp, assoc, assoc, œà.comm‚ÇÇ‚ÇÉ]

/-- Homotopy between morphisms of short complexes is compatible with composition. -/
@[simps!]
def comp (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) {œà‚ÇÅ œà‚ÇÇ : S‚ÇÇ ‚ü∂ S‚ÇÉ} (h' : Homotopy œà‚ÇÅ œà‚ÇÇ) :
    Homotopy (œÜ‚ÇÅ ‚â´ œà‚ÇÅ) (œÜ‚ÇÇ ‚â´ œà‚ÇÇ) :=
  (h.compRight œà‚ÇÅ).trans (h'.compLeft œÜ‚ÇÇ)

/-- The homotopy between morphisms in `ShortComplex C·µí·µñ` that is induced by a homotopy
between morphisms in `ShortComplex C`. -/
@[simps]
def op (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) : Homotopy (opMap œÜ‚ÇÅ) (opMap œÜ‚ÇÇ) where
  h‚ÇÄ := h.h‚ÇÉ.op
  h‚ÇÅ := h.h‚ÇÇ.op
  h‚ÇÇ := h.h‚ÇÅ.op
  h‚ÇÉ := h.h‚ÇÄ.op
  h‚ÇÄ_f := Quiver.Hom.unop_inj h.g_h‚ÇÉ
  g_h‚ÇÉ := Quiver.Hom.unop_inj h.h‚ÇÄ_f
  comm‚ÇÅ := Quiver.Hom.unop_inj (by dsimp; rw [h.comm‚ÇÉ]; abel)
  comm‚ÇÇ := Quiver.Hom.unop_inj (by dsimp; rw [h.comm‚ÇÇ]; abel)
  comm‚ÇÉ := Quiver.Hom.unop_inj (by dsimp; rw [h.comm‚ÇÅ]; abel)

/-- The homotopy between morphisms in `ShortComplex C` that is induced by a homotopy
between morphisms in `ShortComplex C·µí·µñ`. -/
@[simps]
def unop {S‚ÇÅ S‚ÇÇ : ShortComplex C·µí·µñ} {œÜ‚ÇÅ œÜ‚ÇÇ : S‚ÇÅ ‚ü∂ S‚ÇÇ}  (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) :
    Homotopy (unopMap œÜ‚ÇÅ) (unopMap œÜ‚ÇÇ) where
  h‚ÇÄ := h.h‚ÇÉ.unop
  h‚ÇÅ := h.h‚ÇÇ.unop
  h‚ÇÇ := h.h‚ÇÅ.unop
  h‚ÇÉ := h.h‚ÇÄ.unop
  h‚ÇÄ_f := Quiver.Hom.op_inj h.g_h‚ÇÉ
  g_h‚ÇÉ := Quiver.Hom.op_inj h.h‚ÇÄ_f
  comm‚ÇÅ := Quiver.Hom.op_inj (by dsimp; rw [h.comm‚ÇÉ]; abel)
  comm‚ÇÇ := Quiver.Hom.op_inj (by dsimp; rw [h.comm‚ÇÇ]; abel)
  comm‚ÇÉ := Quiver.Hom.op_inj (by dsimp; rw [h.comm‚ÇÅ]; abel)

variable (œÜ‚ÇÅ œÜ‚ÇÇ)

/-- Equivalence expressing that two morphisms are homotopic iff
their difference is homotopic to zero. -/
@[simps]
def equivSubZero : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ ‚âÉ Homotopy (œÜ‚ÇÅ - œÜ‚ÇÇ) 0 where
  toFun h := (h.sub (refl œÜ‚ÇÇ)).trans (ofEq (sub_self œÜ‚ÇÇ))
  invFun h := ((ofEq (sub_add_cancel œÜ‚ÇÅ œÜ‚ÇÇ).symm).trans
    (h.add (refl œÜ‚ÇÇ))).trans (ofEq (zero_add œÜ‚ÇÇ))
  left_inv := by aesop_cat
  right_inv := by aesop_cat

variable {œÜ‚ÇÅ œÜ‚ÇÇ}

lemma eq_add_nullHomotopic (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) :
    œÜ‚ÇÅ = œÜ‚ÇÇ + nullHomotopic _ _ h.h‚ÇÄ h.h‚ÇÄ_f h.h‚ÇÅ h.h‚ÇÇ h.h‚ÇÉ h.g_h‚ÇÉ := by
  ext
  ¬∑ dsimp; rw [h.comm‚ÇÅ]; abel
  ¬∑ dsimp; rw [h.comm‚ÇÇ]; abel
  ¬∑ dsimp; rw [h.comm‚ÇÉ]; abel

variable (S‚ÇÅ S‚ÇÇ)

/-- A morphism constructed with `nullHomotopic` is homotopic to zero. -/
@[simps]
def ofNullHomotopic (h‚ÇÄ : S‚ÇÅ.X‚ÇÅ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÄ_f : h‚ÇÄ ‚â´ S‚ÇÇ.f = 0)
    (h‚ÇÅ : S‚ÇÅ.X‚ÇÇ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÇ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÇ) (h‚ÇÉ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÉ) (g_h‚ÇÉ : S‚ÇÅ.g ‚â´ h‚ÇÉ = 0) :
  Homotopy (nullHomotopic _ _ h‚ÇÄ h‚ÇÄ_f h‚ÇÅ h‚ÇÇ h‚ÇÉ g_h‚ÇÉ) 0 where
  h‚ÇÄ := h‚ÇÄ
  h‚ÇÅ := h‚ÇÅ
  h‚ÇÇ := h‚ÇÇ
  h‚ÇÉ := h‚ÇÉ
  h‚ÇÄ_f := h‚ÇÄ_f
  g_h‚ÇÉ := g_h‚ÇÉ
  comm‚ÇÅ := by rw [nullHomotopic_œÑ‚ÇÅ, zero_œÑ‚ÇÅ, add_zero]; abel
  comm‚ÇÇ := by rw [nullHomotopic_œÑ‚ÇÇ, zero_œÑ‚ÇÇ, add_zero]; abel
  comm‚ÇÉ := by rw [nullHomotopic_œÑ‚ÇÉ, zero_œÑ‚ÇÉ, add_zero]; abel

end Homotopy

variable {S‚ÇÅ S‚ÇÇ}

/-- The left homology map data expressing that null homotopic maps induce the zero
morphism in left homology. -/
def LeftHomologyMapData.ofNullHomotopic
    (H‚ÇÅ : S‚ÇÅ.LeftHomologyData) (H‚ÇÇ : S‚ÇÇ.LeftHomologyData)
    (h‚ÇÄ : S‚ÇÅ.X‚ÇÅ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÄ_f : h‚ÇÄ ‚â´ S‚ÇÇ.f = 0)
    (h‚ÇÅ : S‚ÇÅ.X‚ÇÇ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÇ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÇ) (h‚ÇÉ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÉ) (g_h‚ÇÉ : S‚ÇÅ.g ‚â´ h‚ÇÉ = 0) :
    LeftHomologyMapData (nullHomotopic _ _ h‚ÇÄ h‚ÇÄ_f h‚ÇÅ h‚ÇÇ h‚ÇÉ g_h‚ÇÉ) H‚ÇÅ H‚ÇÇ where
  œÜK := H‚ÇÇ.liftK (H‚ÇÅ.i ‚â´ h‚ÇÅ ‚â´ S‚ÇÇ.f) (by simp)
  œÜH := 0
  commf' := by
    rw [‚Üê cancel_mono H‚ÇÇ.i, assoc, LeftHomologyData.liftK_i, LeftHomologyData.f'_i_assoc,
      nullHomotopic_œÑ‚ÇÅ, add_comp, add_comp, assoc, assoc, assoc, LeftHomologyData.f'_i,
      self_eq_add_left, h‚ÇÄ_f]
  commœÄ := by
    rw [H‚ÇÇ.liftK_œÄ_eq_zero_of_boundary (H‚ÇÅ.i ‚â´ h‚ÇÅ ‚â´ S‚ÇÇ.f) (H‚ÇÅ.i ‚â´ h‚ÇÅ) (by rw [assoc]), comp_zero]

/-- The right homology map data expressing that null homotopic maps induce the zero
morphism in right homology. -/
def RightHomologyMapData.ofNullHomotopic
    (H‚ÇÅ : S‚ÇÅ.RightHomologyData) (H‚ÇÇ : S‚ÇÇ.RightHomologyData)
    (h‚ÇÄ : S‚ÇÅ.X‚ÇÅ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÄ_f : h‚ÇÄ ‚â´ S‚ÇÇ.f = 0)
    (h‚ÇÅ : S‚ÇÅ.X‚ÇÇ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÇ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÇ) (h‚ÇÉ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÉ) (g_h‚ÇÉ : S‚ÇÅ.g ‚â´ h‚ÇÉ = 0) :
    RightHomologyMapData (nullHomotopic _ _ h‚ÇÄ h‚ÇÄ_f h‚ÇÅ h‚ÇÇ h‚ÇÉ g_h‚ÇÉ) H‚ÇÅ H‚ÇÇ where
  œÜQ := H‚ÇÅ.descQ (S‚ÇÅ.g ‚â´ h‚ÇÇ ‚â´ H‚ÇÇ.p) (by simp)
  œÜH := 0
  commg' := by
    rw [‚Üê cancel_epi H‚ÇÅ.p, RightHomologyData.p_descQ_assoc, RightHomologyData.p_g'_assoc,
      nullHomotopic_œÑ‚ÇÉ, comp_add, assoc, assoc, RightHomologyData.p_g', g_h‚ÇÉ, add_zero]
  commŒπ := by
    rw [H‚ÇÅ.Œπ_descQ_eq_zero_of_boundary (S‚ÇÅ.g ‚â´ h‚ÇÇ ‚â´ H‚ÇÇ.p) (h‚ÇÇ ‚â´ H‚ÇÇ.p) rfl, zero_comp]

@[simp]
lemma leftHomologyMap'_nullHomotopic
    (H‚ÇÅ : S‚ÇÅ.LeftHomologyData) (H‚ÇÇ : S‚ÇÇ.LeftHomologyData)
    (h‚ÇÄ : S‚ÇÅ.X‚ÇÅ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÄ_f : h‚ÇÄ ‚â´ S‚ÇÇ.f = 0)
    (h‚ÇÅ : S‚ÇÅ.X‚ÇÇ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÇ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÇ) (h‚ÇÉ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÉ) (g_h‚ÇÉ : S‚ÇÅ.g ‚â´ h‚ÇÉ = 0) :
    leftHomologyMap' (nullHomotopic _ _ h‚ÇÄ h‚ÇÄ_f h‚ÇÅ h‚ÇÇ h‚ÇÉ g_h‚ÇÉ) H‚ÇÅ H‚ÇÇ = 0 :=
  (LeftHomologyMapData.ofNullHomotopic H‚ÇÅ H‚ÇÇ h‚ÇÄ h‚ÇÄ_f h‚ÇÅ h‚ÇÇ h‚ÇÉ g_h‚ÇÉ).leftHomologyMap'_eq

@[simp]
lemma rightHomologyMap'_nullHomotopic
    (H‚ÇÅ : S‚ÇÅ.RightHomologyData) (H‚ÇÇ : S‚ÇÇ.RightHomologyData)
    (h‚ÇÄ : S‚ÇÅ.X‚ÇÅ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÄ_f : h‚ÇÄ ‚â´ S‚ÇÇ.f = 0)
    (h‚ÇÅ : S‚ÇÅ.X‚ÇÇ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÇ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÇ) (h‚ÇÉ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÉ) (g_h‚ÇÉ : S‚ÇÅ.g ‚â´ h‚ÇÉ = 0) :
    rightHomologyMap' (nullHomotopic _ _ h‚ÇÄ h‚ÇÄ_f h‚ÇÅ h‚ÇÇ h‚ÇÉ g_h‚ÇÉ) H‚ÇÅ H‚ÇÇ = 0 :=
  (RightHomologyMapData.ofNullHomotopic H‚ÇÅ H‚ÇÇ h‚ÇÄ h‚ÇÄ_f h‚ÇÅ h‚ÇÇ h‚ÇÉ g_h‚ÇÉ).rightHomologyMap'_eq

@[simp]
lemma homologyMap'_nullHomotopic
    (H‚ÇÅ : S‚ÇÅ.HomologyData) (H‚ÇÇ : S‚ÇÇ.HomologyData)
    (h‚ÇÄ : S‚ÇÅ.X‚ÇÅ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÄ_f : h‚ÇÄ ‚â´ S‚ÇÇ.f = 0)
    (h‚ÇÅ : S‚ÇÅ.X‚ÇÇ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÇ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÇ) (h‚ÇÉ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÉ) (g_h‚ÇÉ : S‚ÇÅ.g ‚â´ h‚ÇÉ = 0) :
    homologyMap' (nullHomotopic _ _ h‚ÇÄ h‚ÇÄ_f h‚ÇÅ h‚ÇÇ h‚ÇÉ g_h‚ÇÉ) H‚ÇÅ H‚ÇÇ = 0 := by
  apply leftHomologyMap'_nullHomotopic

variable (S‚ÇÅ S‚ÇÇ)

@[simp]
lemma leftHomologyMap_nullHomotopic [S‚ÇÅ.HasLeftHomology] [S‚ÇÇ.HasLeftHomology]
    (h‚ÇÄ : S‚ÇÅ.X‚ÇÅ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÄ_f : h‚ÇÄ ‚â´ S‚ÇÇ.f = 0)
    (h‚ÇÅ : S‚ÇÅ.X‚ÇÇ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÇ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÇ) (h‚ÇÉ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÉ) (g_h‚ÇÉ : S‚ÇÅ.g ‚â´ h‚ÇÉ = 0) :
    leftHomologyMap (nullHomotopic _ _ h‚ÇÄ h‚ÇÄ_f h‚ÇÅ h‚ÇÇ h‚ÇÉ g_h‚ÇÉ) = 0 := by
  apply leftHomologyMap'_nullHomotopic

@[simp]
lemma rightHomologyMap_nullHomotopic [S‚ÇÅ.HasRightHomology] [S‚ÇÇ.HasRightHomology]
    (h‚ÇÄ : S‚ÇÅ.X‚ÇÅ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÄ_f : h‚ÇÄ ‚â´ S‚ÇÇ.f = 0)
    (h‚ÇÅ : S‚ÇÅ.X‚ÇÇ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÇ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÇ) (h‚ÇÉ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÉ) (g_h‚ÇÉ : S‚ÇÅ.g ‚â´ h‚ÇÉ = 0) :
    rightHomologyMap (nullHomotopic _ _ h‚ÇÄ h‚ÇÄ_f h‚ÇÅ h‚ÇÇ h‚ÇÉ g_h‚ÇÉ) = 0 := by
  apply rightHomologyMap'_nullHomotopic

@[simp]
lemma homologyMap_nullHomotopic [S‚ÇÅ.HasHomology] [S‚ÇÇ.HasHomology]
    (h‚ÇÄ : S‚ÇÅ.X‚ÇÅ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÄ_f : h‚ÇÄ ‚â´ S‚ÇÇ.f = 0)
    (h‚ÇÅ : S‚ÇÅ.X‚ÇÇ ‚ü∂ S‚ÇÇ.X‚ÇÅ) (h‚ÇÇ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÇ) (h‚ÇÉ : S‚ÇÅ.X‚ÇÉ ‚ü∂ S‚ÇÇ.X‚ÇÉ) (g_h‚ÇÉ : S‚ÇÅ.g ‚â´ h‚ÇÉ = 0) :
    homologyMap (nullHomotopic _ _ h‚ÇÄ h‚ÇÄ_f h‚ÇÅ h‚ÇÇ h‚ÇÉ g_h‚ÇÉ) = 0 := by
  apply homologyMap'_nullHomotopic

namespace Homotopy

variable {œÜ‚ÇÅ œÜ‚ÇÇ S‚ÇÅ S‚ÇÇ}

lemma leftHomologyMap'_congr (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) (h‚ÇÅ : S‚ÇÅ.LeftHomologyData)
    (h‚ÇÇ : S‚ÇÇ.LeftHomologyData) : leftHomologyMap' œÜ‚ÇÅ h‚ÇÅ h‚ÇÇ = leftHomologyMap' œÜ‚ÇÇ h‚ÇÅ h‚ÇÇ := by
  rw [h.eq_add_nullHomotopic, leftHomologyMap'_add, leftHomologyMap'_nullHomotopic, add_zero]

lemma rightHomologyMap'_congr (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) (h‚ÇÅ : S‚ÇÅ.RightHomologyData)
    (h‚ÇÇ : S‚ÇÇ.RightHomologyData) : rightHomologyMap' œÜ‚ÇÅ h‚ÇÅ h‚ÇÇ = rightHomologyMap' œÜ‚ÇÇ h‚ÇÅ h‚ÇÇ := by
  rw [h.eq_add_nullHomotopic, rightHomologyMap'_add, rightHomologyMap'_nullHomotopic, add_zero]

lemma homologyMap'_congr (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) (h‚ÇÅ : S‚ÇÅ.HomologyData)
    (h‚ÇÇ : S‚ÇÇ.HomologyData) : homologyMap' œÜ‚ÇÅ h‚ÇÅ h‚ÇÇ = homologyMap' œÜ‚ÇÇ h‚ÇÅ h‚ÇÇ := by
  rw [h.eq_add_nullHomotopic, homologyMap'_add, homologyMap'_nullHomotopic, add_zero]

lemma leftHomologyMap_congr (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) [S‚ÇÅ.HasLeftHomology] [S‚ÇÇ.HasLeftHomology] :
    leftHomologyMap œÜ‚ÇÅ = leftHomologyMap œÜ‚ÇÇ :=
  h.leftHomologyMap'_congr _ _

lemma rightHomologyMap_congr (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) [S‚ÇÅ.HasRightHomology] [S‚ÇÇ.HasRightHomology] :
    rightHomologyMap œÜ‚ÇÅ = rightHomologyMap œÜ‚ÇÇ :=
  h.rightHomologyMap'_congr _ _

lemma homologyMap_congr (h : Homotopy œÜ‚ÇÅ œÜ‚ÇÇ) [S‚ÇÅ.HasHomology] [S‚ÇÇ.HasHomology] :
    homologyMap œÜ‚ÇÅ = homologyMap œÜ‚ÇÇ :=
  h.homologyMap'_congr _ _

end Homotopy

/-- An homotopy equivalence between two short complexes `S‚ÇÅ` and `S‚ÇÇ` consists
of morphisms `hom : S‚ÇÅ ‚ü∂ S‚ÇÇ` and `inv : S‚ÇÇ ‚ü∂ S‚ÇÅ` such that both compositions
`hom ‚â´ inv` and `inv ‚â´ hom` are homotopic to the identity. -/
@[ext]
structure HomotopyEquiv where
  /-- the forward direction of a homotopy equivalence. -/
  hom : S‚ÇÅ ‚ü∂ S‚ÇÇ
  /-- the backwards direction of a homotopy equivalence. -/
  inv : S‚ÇÇ ‚ü∂ S‚ÇÅ
  /-- the composition of the two directions of a homotopy equivalence is
  homotopic to the identity of the source -/
  homotopyHomInvId : Homotopy (hom ‚â´ inv) (ùüô S‚ÇÅ)
  /-- the composition of the two directions of a homotopy equivalence is
  homotopic to the identity of the target -/
  homotopyInvHomId : Homotopy (inv ‚â´ hom) (ùüô S‚ÇÇ)

namespace HomotopyEquiv

variable {S‚ÇÅ S‚ÇÇ}

/-- The homotopy equivalence from a short complex to itself that is induced
by the identity. -/
@[simps]
def refl (S : ShortComplex C) : HomotopyEquiv S S where
  hom := ùüô S
  inv := ùüô S
  homotopyHomInvId := Homotopy.ofEq (by simp)
  homotopyInvHomId := Homotopy.ofEq (by simp)

/-- The inverse of a homotopy equivalence. -/
@[simps]
def symm (e : HomotopyEquiv S‚ÇÅ S‚ÇÇ) : HomotopyEquiv S‚ÇÇ S‚ÇÅ where
  hom := e.inv
  inv := e.hom
  homotopyHomInvId := e.homotopyInvHomId
  homotopyInvHomId := e.homotopyHomInvId

/-- The composition of homotopy equivalences. -/
@[simps]
def trans (e : HomotopyEquiv S‚ÇÅ S‚ÇÇ) (e' : HomotopyEquiv S‚ÇÇ S‚ÇÉ) :
    HomotopyEquiv S‚ÇÅ S‚ÇÉ where
  hom := e.hom ‚â´ e'.hom
  inv := e'.inv ‚â´ e.inv
  homotopyHomInvId := (Homotopy.ofEq (by simp)).trans
    (((e'.homotopyHomInvId.compRight e.inv).compLeft e.hom).trans
      ((Homotopy.ofEq (by simp)).trans e.homotopyHomInvId))
  homotopyInvHomId := (Homotopy.ofEq (by simp)).trans
    (((e.homotopyInvHomId.compRight e'.hom).compLeft e'.inv).trans
      ((Homotopy.ofEq (by simp)).trans e'.homotopyInvHomId))

end HomotopyEquiv

end Homotopy

end ShortComplex

end CategoryTheory
