/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/

import Mathlib.Algebra.Homology.ImageToKernel
import Mathlib.Algebra.Homology.ShortComplex.Homology
import Mathlib.Algebra.Homology.ShortComplex.Preadditive
import Mathlib.Algebra.Homology.ShortComplex.Limits
import Mathlib.CategoryTheory.Abelian.Basic
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Kernels

/-!
# Abelian categories have homology

In this file, it is shown that all short complexes `S` in abelian
categories have terms of type `S.HomologyData`.

The strategy of the proof is to study the morphism
`kernel.Œπ S.g ‚â´ cokernel.œÄ S.f`. We show that there is a
`LeftHomologyData` for `S` for which the `H` field consists
of the coimage of `kernel.Œπ S.g ‚â´ cokernel.œÄ S.f`, while
there is a `RightHomologyData` for which the `H` is the
image of `kernel.Œπ S.g ‚â´ cokernel.œÄ S.f`. The fact that
these left and right homology data are compatible (i.e.
provide a `HomologyData`) is obtained by using the
coimage-image isomorphism in abelian categories.

-/

universe v' u' v u

namespace CategoryTheory

open Category Limits

variable {C : Type u} [Category.{v} C] [Abelian C] (S : ShortComplex C)
  {D : Type u'} [Category.{v'} D] [HasZeroMorphisms D]

namespace ShortComplex

/-- The canonical morphism `Abelian.image S.f ‚ü∂ kernel S.g` for a short complex `S`
in an abelian category. -/
noncomputable def abelianImageToKernel : Abelian.image S.f ‚ü∂ kernel S.g :=
  kernel.lift S.g (Abelian.image.Œπ S.f)
    (by simp only [‚Üê cancel_epi (Abelian.factorThruImage S.f),
      kernel.lift_Œπ_assoc, zero, comp_zero])

@[reassoc (attr := simp)]
lemma abelianImageToKernel_comp_kernel_Œπ :
    S.abelianImageToKernel ‚â´ kernel.Œπ S.g = Abelian.image.Œπ S.f :=
  kernel.lift_Œπ _ _ _

instance : Mono S.abelianImageToKernel :=
  mono_of_mono_fac S.abelianImageToKernel_comp_kernel_Œπ

@[reassoc (attr := simp 1100)]
lemma abelianImageToKernel_comp_kernel_Œπ_comp_cokernel_œÄ :
    S.abelianImageToKernel ‚â´ kernel.Œπ S.g ‚â´ cokernel.œÄ S.f = 0 := by
  simp only [abelianImageToKernel_comp_kernel_Œπ_assoc, kernel.condition]

/-- `Abelian.image S.f` is the kernel of `kernel.Œπ S.g ‚â´ cokernel.œÄ S.f` -/
noncomputable def abelianImageToKernelIsKernel :
    IsLimit (KernelFork.ofŒπ S.abelianImageToKernel
      S.abelianImageToKernel_comp_kernel_Œπ_comp_cokernel_œÄ) :=
  KernelFork.IsLimit.ofŒπ _ _
    (fun k hk => kernel.lift _ (k ‚â´ kernel.Œπ S.g) (by rw [assoc, hk]))
    (fun k hk => by simp only [‚Üê cancel_mono (kernel.Œπ S.g), assoc,
      abelianImageToKernel_comp_kernel_Œπ, kernel.lift_Œπ])
    (fun k hk b hb => by simp only [‚Üê cancel_mono S.abelianImageToKernel,
      ‚Üê cancel_mono (kernel.Œπ S.g), hb, assoc, abelianImageToKernel_comp_kernel_Œπ, kernel.lift_Œπ])

namespace LeftHomologyData

/-- The canonical `LeftHomologyData` of a short complex `S` in an abelian category, for
which the `H` field is `Abelian.coimage (kernel.Œπ S.g ‚â´ cokernel.œÄ S.f)`. -/
@[simps]
noncomputable def ofAbelian : S.LeftHomologyData := by
  let Œ≥ := kernel.Œπ S.g ‚â´ cokernel.œÄ S.f
  let f' := kernel.lift S.g S.f S.zero
  have hf' : f' = kernel.lift Œ≥ f' (by simp) ‚â´ kernel.Œπ Œ≥ := by rw [kernel.lift_Œπ]
  have wœÄ : f' ‚â´ cokernel.œÄ (kernel.Œπ Œ≥) = 0 := by
    rw [hf']
    simp only [assoc, cokernel.condition, comp_zero]
  let e : Abelian.image S.f ‚âÖ kernel Œ≥ :=
    IsLimit.conePointUniqueUpToIso S.abelianImageToKernelIsKernel (limit.isLimit _)
  have he : e.hom ‚â´ kernel.Œπ Œ≥ = S.abelianImageToKernel :=
    IsLimit.conePointUniqueUpToIso_hom_comp _ _ WalkingParallelPair.zero
  have fac : f' = Abelian.factorThruImage S.f ‚â´ e.hom ‚â´ kernel.Œπ Œ≥ := by
    rw [hf', he]
    simp only [kernel.lift_Œπ, abelianImageToKernel, ‚Üê cancel_mono (kernel.Œπ S.g), assoc]
  have hœÄ : IsColimit (CokernelCofork.ofœÄ _ wœÄ) :=
    CokernelCofork.IsColimit.ofœÄ _ _
    (fun x hx => cokernel.desc _ x (by
      simpa only [‚Üê cancel_epi e.hom, ‚Üê cancel_epi (Abelian.factorThruImage S.f),
        comp_zero, fac, assoc] using hx))
    (fun x hx => cokernel.œÄ_desc _ _ _)
    (fun x hx b hb => coequalizer.hom_ext (by simp only [hb, cokernel.œÄ_desc]))
  exact
    { K := kernel S.g,
      H := Abelian.coimage (kernel.Œπ S.g ‚â´ cokernel.œÄ S.f)
      i := kernel.Œπ _,
      œÄ := cokernel.œÄ _
      wi := kernel.condition _
      hi := kernelIsKernel _
      wœÄ := wœÄ
      hœÄ := hœÄ }

end LeftHomologyData

/-- The canonical morphism `cokernel S.f ‚ü∂ Abelian.coimage S.g` for a short complex `S`
in an abelian category. -/
noncomputable def cokernelToAbelianCoimage : cokernel S.f ‚ü∂ Abelian.coimage S.g :=
  cokernel.desc S.f (Abelian.coimage.œÄ S.g) (by
    simp only [‚Üê cancel_mono (Abelian.factorThruCoimage S.g), assoc,
      cokernel.œÄ_desc, zero, zero_comp])

@[reassoc (attr := simp)]
lemma cokernel_œÄ_comp_cokernelToAbelianCoimage :
    cokernel.œÄ S.f ‚â´ S.cokernelToAbelianCoimage = Abelian.coimage.œÄ S.g :=
  cokernel.œÄ_desc _ _ _

instance : Epi S.cokernelToAbelianCoimage :=
  epi_of_epi_fac S.cokernel_œÄ_comp_cokernelToAbelianCoimage

lemma kernel_Œπ_comp_cokernel_œÄ_comp_cokernelToAbelianCoimage :
    (kernel.Œπ S.g ‚â´ cokernel.œÄ S.f) ‚â´ S.cokernelToAbelianCoimage = 0 := by simp

/-- `Abelian.coimage S.g` is the cokernel of `kernel.Œπ S.g ‚â´ cokernel.œÄ S.f` -/
noncomputable def cokernelToAbelianCoimageIsCokernel :
    IsColimit (CokernelCofork.ofœÄ S.cokernelToAbelianCoimage
      S.kernel_Œπ_comp_cokernel_œÄ_comp_cokernelToAbelianCoimage) :=
  CokernelCofork.IsColimit.ofœÄ _ _
    (fun k hk => cokernel.desc _ (cokernel.œÄ S.f ‚â´ k) (by simpa only [assoc] using hk))
    (fun k hk => by simp only [‚Üê cancel_epi (cokernel.œÄ S.f),
        cokernel_œÄ_comp_cokernelToAbelianCoimage_assoc, cokernel.œÄ_desc])
    (fun k hk b hb => by
      simp only [‚Üê cancel_epi S.cokernelToAbelianCoimage, ‚Üê cancel_epi (cokernel.œÄ S.f), hb,
        cokernel_œÄ_comp_cokernelToAbelianCoimage_assoc, cokernel.œÄ_desc])

namespace RightHomologyData

/-- The canonical `RightHomologyData` of a short complex `S` in an abelian category, for
which the `H` field is `Abelian.image (kernel.Œπ S.g ‚â´ cokernel.œÄ S.f)`. -/
@[simps]
noncomputable def ofAbelian : S.RightHomologyData := by
  let Œ≥ := kernel.Œπ S.g ‚â´ cokernel.œÄ S.f
  let g' := cokernel.desc S.f S.g S.zero
  have hg' : g' = cokernel.œÄ Œ≥ ‚â´ cokernel.desc Œ≥ g' (by simp) := by rw [cokernel.œÄ_desc]
  have wŒπ : kernel.Œπ (cokernel.œÄ Œ≥) ‚â´ g' = 0 := by rw [hg', kernel.condition_assoc, zero_comp]
  let e : cokernel Œ≥ ‚âÖ Abelian.coimage S.g :=
    IsColimit.coconePointUniqueUpToIso (colimit.isColimit _) S.cokernelToAbelianCoimageIsCokernel
  have he : cokernel.œÄ Œ≥ ‚â´ e.hom = S.cokernelToAbelianCoimage :=
    IsColimit.comp_coconePointUniqueUpToIso_hom _ _ WalkingParallelPair.one
  have fac : g' = cokernel.œÄ Œ≥ ‚â´ e.hom ‚â´ Abelian.factorThruCoimage S.g := by
    rw [hg', reassoc_of% he]
    simp only [cokernel.œÄ_desc, ‚Üê cancel_epi (cokernel.œÄ S.f),
      cokernel_œÄ_comp_cokernelToAbelianCoimage_assoc]
  have hŒπ : IsLimit (KernelFork.ofŒπ _ wŒπ) :=
    KernelFork.IsLimit.ofŒπ _ _
      (fun x hx => kernel.lift _ x (by
        simpa only [‚Üê cancel_mono e.hom, ‚Üê cancel_mono (Abelian.factorThruCoimage S.g), assoc,
          zero_comp, fac] using hx))
      (fun x hx => kernel.lift_Œπ _ _ _)
      (fun x hx b hb => equalizer.hom_ext (by simp only [hb, kernel.lift_Œπ]))
  exact
    { Q := cokernel S.f,
      H := Abelian.image (kernel.Œπ S.g ‚â´ cokernel.œÄ S.f)
      p := cokernel.œÄ _
      Œπ := kernel.Œπ _
      wp := cokernel.condition _
      hp := cokernelIsCokernel _
      wŒπ := wŒπ
      hŒπ := hŒπ }

end RightHomologyData

/-- The canonical `HomologyData` of a short complex `S` in an abelian category. -/
noncomputable def HomologyData.ofAbelian : S.HomologyData where
  left := LeftHomologyData.ofAbelian S
  right := RightHomologyData.ofAbelian S
  iso := Abelian.coimageIsoImage (kernel.Œπ S.g ‚â´ cokernel.œÄ S.f)

instance _root_.CategoryTheory.categoryWithHomology_of_abelian :
    CategoryWithHomology C where
  hasHomology S := HasHomology.mk' (HomologyData.ofAbelian S)

noncomputable def _root_.CategoryTheory.Limits.isLimit_mapCone_of_kernelFork_ofŒπ_cokernel_condition_of_mono
    {X Y : D} (i : X ‚ü∂ Y) [HasCokernel i] (F : D ‚•§ C)
    [F.PreservesZeroMorphisms] [Mono (F.map i)]
    [PreservesColimit (parallelPair i 0) F] :
    IsLimit (F.mapCone (KernelFork.ofŒπ i (cokernel.condition i))) := by
  let e : parallelPair (cokernel.œÄ (F.map i)) 0 ‚âÖ parallelPair (cokernel.œÄ i) 0 ‚ãô F :=
    parallelPair.ext (Iso.refl _) (asIso (cokernelComparison i F)) (by simp) (by simp)
  refine' IsLimit.postcomposeInvEquiv e _ _
  let hi := Abelian.monoIsKernelOfCokernel _ (cokernelIsCokernel (F.map i))
  refine' IsLimit.ofIsoLimit hi (Fork.ext (Iso.refl _) _)
  change ùüô _ ‚â´ F.map i ‚â´ ùüô _ = F.map i
  rw [comp_id, id_comp]

noncomputable instance : NormalMonoCategory (ShortComplex C) := ‚ü®fun i _ => by
  refine' NormalMono.mk _ (cokernel.œÄ i) (cokernel.condition _)
    (isLimitOfIsLimitœÄ _ _ _ _ )
  all_goals apply isLimit_mapCone_of_kernelFork_ofŒπ_cokernel_condition_of_mono‚ü©

noncomputable def _root_.CategoryTheory.Limits.isColimit_mapCocone_of_cokernelCofork_ofœÄ_kernel_condition_of_epi
    {X Y : D} (p : X ‚ü∂ Y) [HasKernel p] (F : D ‚•§ C)
    [F.PreservesZeroMorphisms] [Epi (F.map p)]
    [PreservesLimit (parallelPair p 0) F] :
    IsColimit (F.mapCocone (CokernelCofork.ofœÄ p (kernel.condition p))) := by
  let e : parallelPair (kernel.Œπ p) 0 ‚ãô F ‚âÖ parallelPair (kernel.Œπ (F.map p)) 0 := by
    refine' parallelPair.ext (asIso (kernelComparison p F)) (Iso.refl _) (by simp) (by simp)
  refine' IsColimit.precomposeInvEquiv e _ _
  let hp := Abelian.epiIsCokernelOfKernel _ (kernelIsKernel (F.map p))
  refine' IsColimit.ofIsoColimit hp (Cofork.ext (Iso.refl _) _)
  change F.map p ‚â´ ùüô _ = ùüô _ ‚â´ F.map p
  rw [comp_id, id_comp]

noncomputable instance : NormalEpiCategory (ShortComplex C) := ‚ü®fun p _ => by
  refine' NormalEpi.mk _ (kernel.Œπ p) (kernel.condition _)
    (isColimitOfIsColimitœÄ _ _ _ _ )
  all_goals apply isColimit_mapCocone_of_cokernelCofork_ofœÄ_kernel_condition_of_epi‚ü©

noncomputable instance : Abelian (ShortComplex C) where

attribute [local instance] strongEpi_of_epi

noncomputable def homologyIsoImageICyclesCompPOpcycles :
    S.homology ‚âÖ image (S.iCycles ‚â´ S.pOpcycles) :=
  image.isoStrongEpiMono _ _ S.homology_œÄ_Œπ

@[reassoc (attr := simp)]
lemma homologyIsoImageICyclesCompPOpcycles_Œπ :
    S.homologyIsoImageICyclesCompPOpcycles.hom ‚â´ image.Œπ (S.iCycles ‚â´ S.pOpcycles) =
      S.homologyŒπ :=
  image.isoStrongEpiMono_hom_comp_Œπ _ _ _

namespace HomologyData

namespace OfEpiMonoFactorisation

variable {kf : KernelFork S.g} {cc : CokernelCofork S.f}
  (hkf : IsLimit kf) (hcc : IsColimit cc)
  {H : C} {œÄ : kf.pt ‚ü∂ H} {Œπ : H ‚ü∂ cc.pt}
  (fac : kf.Œπ ‚â´ cc.œÄ = œÄ ‚â´ Œπ)
  [Epi œÄ] [Mono Œπ]

noncomputable def isoK : kf.pt ‚âÖ S.cycles :=
  IsLimit.conePointUniqueUpToIso hkf S.cyclesIsKernel

@[reassoc (attr := simp)]
lemma isoK_inv_Œπ : (isoK S hkf).inv ‚â´ kf.Œπ = S.iCycles :=
  IsLimit.conePointUniqueUpToIso_inv_comp _ _ WalkingParallelPair.zero

@[reassoc (attr := simp)]
lemma isoK_hom_iCycles : (isoK S hkf).hom ‚â´ S.iCycles = kf.Œπ := by
  rw [‚Üê isoK_inv_Œπ S hkf, Iso.hom_inv_id_assoc]

noncomputable def isoQ : cc.pt ‚âÖ S.opcycles :=
  IsColimit.coconePointUniqueUpToIso hcc S.opcyclesIsCokernel

@[reassoc (attr := simp)]
lemma œÄ_isoQ_hom : cc.œÄ ‚â´ (isoQ S hcc).hom = S.pOpcycles :=
  IsColimit.comp_coconePointUniqueUpToIso_hom _ _ WalkingParallelPair.one

@[reassoc (attr := simp)]
lemma pOpcycles_isoQ_inv : S.pOpcycles ‚â´ (isoQ S hcc).inv = cc.œÄ := by
  rw [‚Üê œÄ_isoQ_hom S hcc, assoc, Iso.hom_inv_id, comp_id]

lemma fac' : ((isoK S hkf).inv ‚â´ œÄ) ‚â´ Œπ ‚â´ (isoQ S hcc).hom = S.iCycles ‚â´ S.pOpcycles := by
  simp only [assoc, ‚Üê reassoc_of% fac, œÄ_isoQ_hom, isoK_inv_Œπ_assoc]

noncomputable def isoImage : H ‚âÖ image (S.iCycles ‚â´ S.pOpcycles) := by
  have := epi_comp (isoK S hkf).inv œÄ
  have := mono_comp Œπ (isoQ S hcc).hom
  exact image.isoStrongEpiMono _ _ (fac' S hkf hcc fac)

@[reassoc (attr := simp)]
lemma isoImage_Œπ :
    (isoImage S hkf hcc fac).hom ‚â´ image.Œπ (S.iCycles ‚â´ S.pOpcycles) =
      Œπ ‚â´ (isoQ S hcc).hom := by
  have := epi_comp (isoK S hkf).inv œÄ
  have := mono_comp Œπ (isoQ S hcc).hom
  apply image.isoStrongEpiMono_hom_comp_Œπ
  simp only [assoc, ‚Üê reassoc_of% fac, œÄ_isoQ_hom, isoK_inv_Œπ_assoc]

noncomputable def isoHomology : H ‚âÖ S.homology :=
  isoImage S hkf hcc fac ‚â™‚â´ S.homologyIsoImageICyclesCompPOpcycles.symm

@[reassoc (attr := simp)]
lemma œÄ_comp_isoHomology_hom :
    œÄ ‚â´ (isoHomology S hkf hcc fac).hom = (isoK S hkf).hom ‚â´ S.homologyœÄ := by
  dsimp [isoHomology]
  simp only [‚Üê cancel_mono (S.homologyIsoImageICyclesCompPOpcycles.hom), assoc,
    Iso.inv_hom_id, comp_id, ‚Üê cancel_mono (image.Œπ (S.iCycles ‚â´ S.pOpcycles)),
    isoImage_Œπ, homologyIsoImageICyclesCompPOpcycles_Œπ, homology_œÄ_Œπ, ‚Üê reassoc_of% fac,
    œÄ_isoQ_hom, isoK_hom_iCycles_assoc]

@[reassoc (attr := simp)]
lemma isoHomology_hom_comp_Œπ :
    (isoHomology S hkf hcc fac).inv ‚â´ Œπ = S.homologyŒπ ‚â´ (isoQ S hcc).inv := by
  rw [‚Üê cancel_epi S.homologyœÄ, ‚Üê cancel_epi (isoK S hkf).hom,
    homology_œÄ_Œπ_assoc, ‚Üê œÄ_comp_isoHomology_hom_assoc S hkf hcc fac, Iso.hom_inv_id_assoc,
    ‚Üê fac, isoK_hom_iCycles_assoc, pOpcycles_isoQ_inv]

lemma f'_eq : hkf.lift (KernelFork.ofŒπ S.f S.zero) = S.toCycles ‚â´ (isoK S hkf).inv := by
  have : Mono kf.Œπ := ‚ü®fun _ _ h => Fork.IsLimit.hom_ext hkf h‚ü©
  rw [‚Üê cancel_mono kf.Œπ]
  simp only [Fork.ofŒπ_pt, Fork.IsLimit.lift_Œπ, Fork.Œπ_ofŒπ, assoc,
    isoK_inv_Œπ, toCycles_i]

lemma g'_eq : hcc.desc (CokernelCofork.ofœÄ S.g S.zero) =
    (isoQ S hcc).hom ‚â´ S.fromOpcycles := by
  have : Epi cc.œÄ := ‚ü®fun _ _ h => Cofork.IsColimit.hom_ext hcc h‚ü©
  rw [‚Üê cancel_epi cc.œÄ]
  simp only [Cofork.IsColimit.œÄ_desc, Cofork.œÄ_ofœÄ, œÄ_isoQ_hom_assoc, p_fromOpcycles]

lemma homologyœÄ_isoHomology_inv :
    S.homologyœÄ ‚â´ (isoHomology S hkf hcc fac).inv = (isoK S hkf).inv ‚â´ œÄ := by
  simp only [‚Üê cancel_mono (isoHomology S hkf hcc fac).hom, assoc, Iso.inv_hom_id, comp_id,
    œÄ_comp_isoHomology_hom, Iso.inv_hom_id_assoc]

lemma isoHomology_inv_homologyŒπ :
    (isoHomology S hkf hcc fac).hom ‚â´ S.homologyŒπ = Œπ ‚â´ (isoQ S hcc).hom := by
  rw [‚Üê cancel_mono (isoQ S hcc).inv, assoc, assoc, Iso.hom_inv_id, comp_id,
    ‚Üê isoHomology_hom_comp_Œπ S hkf hcc fac, Iso.hom_inv_id_assoc]

@[simps]
noncomputable def leftHomologyData : S.LeftHomologyData where
  K := kf.pt
  H := H
  i := kf.Œπ
  œÄ := œÄ
  wi := KernelFork.condition kf
  hi := IsLimit.ofIsoLimit hkf (Fork.ext (Iso.refl _) (by simp))
  wœÄ := by
    dsimp
    rw [‚Üê cancel_mono (isoHomology S hkf hcc fac).hom, assoc, assoc, id_comp,
      œÄ_comp_isoHomology_hom, zero_comp, f'_eq,
      assoc, Iso.inv_hom_id_assoc, toCycles_comp_homologyœÄ]
  hœÄ := by
    dsimp
    let e : parallelPair (hkf.lift (KernelFork.ofŒπ S.f S.zero) ‚â´ ùüô _) 0 ‚âÖ
        parallelPair S.toCycles 0 := parallelPair.ext (Iso.refl _) (isoK S hkf)
          (by dsimp ; rw [f'_eq, assoc, assoc, id_comp, Iso.inv_hom_id, comp_id, id_comp])
          (by dsimp ; simp only [zero_comp, comp_zero])
    refine' IsColimit.precomposeInvEquiv e _ _
    exact IsColimit.ofIsoColimit S.homologyIsCokernel
      (Cofork.ext (isoHomology S hkf hcc fac).symm (homologyœÄ_isoHomology_inv S _ _ _))

@[simps]
noncomputable def rightHomologyData : S.RightHomologyData where
  Q := cc.pt
  H := H
  p := cc.œÄ
  Œπ := Œπ
  wp := CokernelCofork.condition cc
  hp := IsColimit.ofIsoColimit hcc (Cofork.ext (Iso.refl _) (by simp))
  wŒπ := by
    dsimp
    rw [id_comp, g'_eq, ‚Üê cancel_epi (isoHomology S hkf hcc fac).inv, comp_zero,
      isoHomology_hom_comp_Œπ_assoc, Iso.inv_hom_id_assoc, homologyŒπ_comp_fromOpcycles]
  hŒπ := by
    let e : parallelPair (ùüô _ ‚â´ hcc.desc (CokernelCofork.ofœÄ S.g S.zero)) 0 ‚âÖ
        parallelPair S.fromOpcycles 0 := parallelPair.ext (isoQ S hcc) (Iso.refl _)
          (by dsimp; simp only [id_comp, comp_id, g'_eq])
          (by simp)
    refine' IsLimit.postcomposeHomEquiv e _ _
    exact IsLimit.ofIsoLimit S.homologyIsKernel
      (Iso.symm (Fork.ext (isoHomology S hkf hcc fac) (isoHomology_inv_homologyŒπ S hkf hcc fac)))

end OfEpiMonoFactorisation

@[simps]
noncomputable def ofEpiMonoFactorisation {kf : KernelFork S.g} {cc : CokernelCofork S.f}
    (hkf : IsLimit kf) (hcc : IsColimit cc) {H : C} {œÄ : kf.pt ‚ü∂ H} {Œπ : H ‚ü∂ cc.pt}
    (fac : kf.Œπ ‚â´ cc.œÄ = œÄ ‚â´ Œπ) [Epi œÄ] [Mono Œπ] :
    S.HomologyData where
  left := OfEpiMonoFactorisation.leftHomologyData S hkf hcc fac
  right := OfEpiMonoFactorisation.rightHomologyData S hkf hcc fac
  iso := Iso.refl _

end HomologyData

end ShortComplex

end CategoryTheory
