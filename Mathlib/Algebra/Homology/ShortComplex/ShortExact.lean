/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Homology.ShortComplex.Exact
import Mathlib.CategoryTheory.Preadditive.Injective.Basic

/-!
# Short exact short complexes

A short complex `S : ShortComplex C` is short exact (`S.ShortExact`) when it is exact,
`S.f` is a mono and `S.g` is an epi.

-/

namespace CategoryTheory

open Category Limits ZeroObject

variable {C D : Type*} [Category C] [Category D]

namespace ShortComplex

section

variable [HasZeroMorphisms C] [HasZeroMorphisms D]
  (S : ShortComplex C) {S‚ÇÅ S‚ÇÇ : ShortComplex C}

/-- A short complex `S` is short exact if it is exact, `S.f` is a mono and `S.g` is an epi. -/
structure ShortExact : Prop where
  exact : S.Exact
  [mono_f : Mono S.f]
  [epi_g : Epi S.g]

variable {S}

lemma ShortExact.mk' (h : S.Exact) (_ : Mono S.f) (_ : Epi S.g) : S.ShortExact where
  exact := h

lemma shortExact_of_iso (e : S‚ÇÅ ‚âÖ S‚ÇÇ) (h : S‚ÇÅ.ShortExact) : S‚ÇÇ.ShortExact where
  exact := exact_of_iso e h.exact
  mono_f := by
    suffices Mono (S‚ÇÇ.f ‚â´ e.inv.œÑ‚ÇÇ) by
      exact mono_of_mono _ e.inv.œÑ‚ÇÇ
    have := h.mono_f
    rw [‚Üê e.inv.comm‚ÇÅ‚ÇÇ]
    apply mono_comp
  epi_g := by
    suffices Epi (e.hom.œÑ‚ÇÇ ‚â´ S‚ÇÇ.g) by
      exact epi_of_epi e.hom.œÑ‚ÇÇ _
    have := h.epi_g
    rw [e.hom.comm‚ÇÇ‚ÇÉ]
    apply epi_comp

lemma shortExact_iff_of_iso (e : S‚ÇÅ ‚âÖ S‚ÇÇ) : S‚ÇÅ.ShortExact ‚Üî S‚ÇÇ.ShortExact := by
  constructor
  ¬∑ exact shortExact_of_iso e
  ¬∑ exact shortExact_of_iso e.symm

lemma ShortExact.op (h : S.ShortExact) : S.op.ShortExact where
  exact := h.exact.op
  mono_f := by
    have := h.epi_g
    dsimp
    infer_instance
  epi_g := by
    have := h.mono_f
    dsimp
    infer_instance

lemma ShortExact.unop {S : ShortComplex C·µí·µñ} (h : S.ShortExact) : S.unop.ShortExact where
  exact := h.exact.unop
  mono_f := by
    have := h.epi_g
    dsimp
    infer_instance
  epi_g := by
    have := h.mono_f
    dsimp
    infer_instance

variable (S)

lemma shortExact_iff_op : S.ShortExact ‚Üî S.op.ShortExact :=
  ‚ü®ShortExact.op, ShortExact.unop‚ü©

lemma shortExact_iff_unop (S : ShortComplex C·µí·µñ) : S.ShortExact ‚Üî S.unop.ShortExact :=
  S.unop.shortExact_iff_op.symm

variable {S}

lemma ShortExact.map (h : S.ShortExact) (F : C ‚•§ D)
    [F.PreservesZeroMorphisms] [F.PreservesLeftHomologyOf S]
    [F.PreservesRightHomologyOf S] [Mono (F.map S.f)] [Epi (F.map S.g)] :
    (S.map F).ShortExact where
  exact := h.exact.map F
  mono_f := (inferInstance : Mono (F.map S.f))
  epi_g := (inferInstance : Epi (F.map S.g))

lemma ShortExact.map_of_exact (hS : S.ShortExact)
    (F : C ‚•§ D) [F.PreservesZeroMorphisms] [PreservesFiniteLimits F]
    [PreservesFiniteColimits F] : (S.map F).ShortExact := by
  have := hS.mono_f
  have := hS.epi_g
  have := preserves_mono_of_preservesLimit F S.f
  have := preserves_epi_of_preservesColimit F S.g
  exact hS.map F

end

section Preadditive

variable [Preadditive C]

lemma ShortExact.isIso_f_iff {S : ShortComplex C} (hS : S.ShortExact) [Balanced C] :
    IsIso S.f ‚Üî IsZero S.X‚ÇÉ := by
  have := hS.exact.hasZeroObject
  have := hS.mono_f
  have := hS.epi_g
  constructor
  ¬∑ intro hf
    simp only [IsZero.iff_id_eq_zero, ‚Üê cancel_epi S.g, ‚Üê cancel_epi S.f,
      S.zero_assoc, zero_comp]
  ¬∑ intro hX‚ÇÉ
    have : Epi S.f := (S.exact_iff_epi (hX‚ÇÉ.eq_of_tgt _ _)).1 hS.exact
    apply isIso_of_mono_of_epi

lemma ShortExact.isIso_g_iff {S : ShortComplex C} (hS : S.ShortExact) [Balanced C] :
    IsIso S.g ‚Üî IsZero S.X‚ÇÅ := by
  have := hS.exact.hasZeroObject
  have := hS.mono_f
  have := hS.epi_g
  constructor
  ¬∑ intro hf
    simp only [IsZero.iff_id_eq_zero, ‚Üê cancel_mono S.f, ‚Üê cancel_mono S.g,
      S.zero, zero_comp, assoc, comp_zero]
  ¬∑ intro hX‚ÇÅ
    have : Mono S.g := (S.exact_iff_mono (hX‚ÇÅ.eq_of_src _ _)).1 hS.exact
    apply isIso_of_mono_of_epi

lemma isIso‚ÇÇ_of_shortExact_of_isIso‚ÇÅ‚ÇÉ [Balanced C] {S‚ÇÅ S‚ÇÇ : ShortComplex C} (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ)
    (h‚ÇÅ : S‚ÇÅ.ShortExact) (h‚ÇÇ : S‚ÇÇ.ShortExact) [IsIso œÜ.œÑ‚ÇÅ] [IsIso œÜ.œÑ‚ÇÉ] : IsIso œÜ.œÑ‚ÇÇ := by
  have := h‚ÇÅ.mono_f
  have := h‚ÇÇ.mono_f
  have := h‚ÇÅ.epi_g
  have := h‚ÇÇ.epi_g
  have := mono_œÑ‚ÇÇ_of_exact_of_mono œÜ h‚ÇÅ.exact
  have := epi_œÑ‚ÇÇ_of_exact_of_epi œÜ h‚ÇÇ.exact
  apply isIso_of_mono_of_epi

lemma isIso‚ÇÇ_of_shortExact_of_isIso‚ÇÅ‚ÇÉ' [Balanced C] {S‚ÇÅ S‚ÇÇ : ShortComplex C} (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ)
    (h‚ÇÅ : S‚ÇÅ.ShortExact) (h‚ÇÇ : S‚ÇÇ.ShortExact) (_ : IsIso œÜ.œÑ‚ÇÅ) (_ : IsIso œÜ.œÑ‚ÇÉ) : IsIso œÜ.œÑ‚ÇÇ :=
  isIso‚ÇÇ_of_shortExact_of_isIso‚ÇÅ‚ÇÉ œÜ h‚ÇÅ h‚ÇÇ

/-- If `S` is a short exact short complex in a balanced category,
then `S.X‚ÇÅ` is the kernel of `S.g`. -/
noncomputable def ShortExact.fIsKernel [Balanced C] {S : ShortComplex C} (hS : S.ShortExact) :
    IsLimit (KernelFork.ofŒπ S.f S.zero) := by
  have := hS.mono_f
  exact hS.exact.fIsKernel

/-- If `S` is a short exact short complex in a balanced category,
then `S.X‚ÇÉ` is the cokernel of `S.f`. -/
noncomputable def ShortExact.gIsCokernel [Balanced C] {S : ShortComplex C} (hS : S.ShortExact) :
    IsColimit (CokernelCofork.ofœÄ S.g S.zero) := by
  have := hS.epi_g
  exact hS.exact.gIsCokernel

/-- Is `S` is an exact short complex and `h : S.HomologyData`, there is
a short exact sequence `0 ‚ü∂ h.left.K ‚ü∂ S.X‚ÇÇ ‚ü∂ h.right.Q ‚ü∂ 0`. -/
lemma Exact.shortExact {S : ShortComplex C} (hS : S.Exact) (h : S.HomologyData) :
    (ShortComplex.mk _ _ (h.exact_iff_i_p_zero.1 hS)).ShortExact where
  exact := by
    have := hS.epi_f' h.left
    have := hS.mono_g' h.right
    let S' := ShortComplex.mk h.left.i S.g (by simp)
    let S'' := ShortComplex.mk _ _ (h.exact_iff_i_p_zero.1 hS)
    let a : S ‚ü∂ S' :=
      { œÑ‚ÇÅ := h.left.f'
        œÑ‚ÇÇ := ùüô _
        œÑ‚ÇÉ := ùüô _ }
    let b : S'' ‚ü∂ S' :=
      { œÑ‚ÇÅ := ùüô _
        œÑ‚ÇÇ := ùüô _
        œÑ‚ÇÉ := h.right.g' }
    rwa [ShortComplex.exact_iff_of_epi_of_isIso_of_mono b,
      ‚Üê ShortComplex.exact_iff_of_epi_of_isIso_of_mono a]

/-- A split short complex is short exact. -/
lemma Splitting.shortExact {S : ShortComplex C} [HasZeroObject C] (s : S.Splitting) :
    S.ShortExact where
  exact := s.exact
  mono_f := s.mono_f
  epi_g := s.epi_g

namespace ShortExact

/-- A choice of splitting for a short exact short complex `S` in a balanced category
such that `S.X‚ÇÅ` is injective. -/
noncomputable def splittingOfInjective {S : ShortComplex C} (hS : S.ShortExact)
    [Injective S.X‚ÇÅ] [Balanced C] :
    S.Splitting :=
  have := hS.mono_f
  Splitting.ofExactOfRetraction S hS.exact (Injective.factorThru (ùüô S.X‚ÇÅ) S.f) (by simp) hS.epi_g

/-- A choice of splitting for a short exact short complex `S` in a balanced category
such that `S.X‚ÇÉ` is projective. -/
noncomputable def splittingOfProjective {S : ShortComplex C} (hS : S.ShortExact)
    [Projective S.X‚ÇÉ] [Balanced C] :
    S.Splitting :=
  have := hS.epi_g
  Splitting.ofExactOfSection S hS.exact (Projective.factorThru (ùüô S.X‚ÇÉ) S.g) (by simp) hS.mono_f

end ShortExact

end Preadditive

end ShortComplex

end CategoryTheory
