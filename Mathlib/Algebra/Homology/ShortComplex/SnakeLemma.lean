/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Homology.ExactSequence
import Mathlib.Algebra.Homology.ShortComplex.Limits
import Mathlib.CategoryTheory.Abelian.Refinements

/-!
# The snake lemma

The snake lemma is a standard tool in homological algebra. The basic situation
is when we have a diagram as follows in an abelian category `C`, with exact rows:

    L‚ÇÅ.X‚ÇÅ ‚ü∂ L‚ÇÅ.X‚ÇÇ ‚ü∂ L‚ÇÅ.X‚ÇÉ ‚ü∂ 0
      |       |       |
      |v‚ÇÅ‚ÇÇ.œÑ‚ÇÅ |v‚ÇÅ‚ÇÇ.œÑ‚ÇÇ |v‚ÇÅ‚ÇÇ.œÑ‚ÇÉ
      v       v       v
0 ‚ü∂ L‚ÇÇ.X‚ÇÅ ‚ü∂ L‚ÇÇ.X‚ÇÇ ‚ü∂ L‚ÇÇ.X‚ÇÉ

We shall think of this diagram as the datum of a morphism `v‚ÇÅ‚ÇÇ : L‚ÇÅ ‚ü∂ L‚ÇÇ` in the
category `ShortComplex C` such that both `L‚ÇÅ` and `L‚ÇÇ` are exact, and `L‚ÇÅ.g` is epi
and `L‚ÇÇ.f` is a mono (which is equivalent to saying that `L‚ÇÅ.X‚ÇÉ` is the cokernel
of `L‚ÇÅ.f` and `L‚ÇÇ.X‚ÇÅ` is the kernel of `L‚ÇÇ.g`). Then, we may introduce the kernels
and cokernels of the vertical maps. In other words, we may introduce short complexes
`L‚ÇÄ` and `L‚ÇÉ` that are respectively the kernel and the cokernel of `v‚ÇÅ‚ÇÇ`. All these
data constitute a `SnakeInput C`.

Given such a `S : SnakeInput C`, we define a connecting homomorphism
`S.Œ¥ : L‚ÇÄ.X‚ÇÉ ‚ü∂ L‚ÇÉ.X‚ÇÅ` and show that it is part of an exact sequence
`L‚ÇÄ.X‚ÇÅ ‚ü∂ L‚ÇÄ.X‚ÇÇ ‚ü∂ L‚ÇÄ.X‚ÇÉ ‚ü∂ L‚ÇÉ.X‚ÇÅ ‚ü∂ L‚ÇÉ.X‚ÇÇ ‚ü∂ L‚ÇÉ.X‚ÇÉ`. Each of the four exactness
statement is first stated separately as lemmas `L‚ÇÄ_exact`, `L‚ÇÅ'_exact`,
`L‚ÇÇ'_exact` and `L‚ÇÉ_exact` and the full 6-term exact sequence is stated
as `snake_lemma`. This sequence can even be extended with an extra `0`
on the left (see `mono_L‚ÇÄ_f`) if `L‚ÇÅ.X‚ÇÅ ‚ü∂ L‚ÇÅ.X‚ÇÇ` is a mono (i.e. `L‚ÇÅ` is short exact),
and similarly an extra `0` can be added on the right (`epi_L‚ÇÉ_g`)
if `L‚ÇÇ.X‚ÇÇ ‚ü∂ L‚ÇÇ.X‚ÇÉ` is an epi (i.e. `L‚ÇÇ` is short exact).

These results were also obtained in the Liquid Tensor Experiment. The code and the proof
here are slightly easier because of the use of the category `ShortComplex C`,
the use of duality (which allows to construct only half of the sequence, and deducing
the other half by arguing in the opposite category), and the use of "refinements"
(see `CategoryTheory.Abelian.Refinements`) instead of a weak form of pseudo-elements.

-/

namespace CategoryTheory

open Category Limits Preadditive

variable (C : Type*) [Category C] [Abelian C]

namespace ShortComplex

/-- A snake input in an abelian category `C` consists of morphisms
of short complexes `L‚ÇÄ ‚ü∂ L‚ÇÅ ‚ü∂ L‚ÇÇ ‚ü∂ L‚ÇÉ` (which should be visualized vertically) such
that `L‚ÇÄ` and `L‚ÇÉ` are respectively the kernel and the cokernel of `L‚ÇÅ ‚ü∂ L‚ÇÇ`,
`L‚ÇÅ` and `L‚ÇÇ` are exact, `L‚ÇÅ.g` is epi and `L‚ÇÇ.f` is mono. -/
structure SnakeInput where
  /-- the zeroth row -/
  L‚ÇÄ : ShortComplex C
  /-- the first row -/
  L‚ÇÅ : ShortComplex C
  /-- the second row -/
  L‚ÇÇ : ShortComplex C
  /-- the third row -/
  L‚ÇÉ : ShortComplex C
  /-- the morphism from the zeroth row to the first row -/
  v‚ÇÄ‚ÇÅ : L‚ÇÄ ‚ü∂ L‚ÇÅ
  /-- the morphism from the first row to the second row -/
  v‚ÇÅ‚ÇÇ : L‚ÇÅ ‚ü∂ L‚ÇÇ
  /-- the morphism from the second row to the third row -/
  v‚ÇÇ‚ÇÉ : L‚ÇÇ ‚ü∂ L‚ÇÉ
  w‚ÇÄ‚ÇÇ : v‚ÇÄ‚ÇÅ ‚â´ v‚ÇÅ‚ÇÇ = 0 := by aesop_cat
  w‚ÇÅ‚ÇÉ : v‚ÇÅ‚ÇÇ ‚â´ v‚ÇÇ‚ÇÉ = 0 := by aesop_cat
  /-- `L‚ÇÄ` is the kernel of `v‚ÇÅ‚ÇÇ : L‚ÇÅ ‚ü∂ L‚ÇÇ`. -/
  h‚ÇÄ : IsLimit (KernelFork.ofŒπ _ w‚ÇÄ‚ÇÇ)
  /-- `L‚ÇÉ` is the cokernel of `v‚ÇÅ‚ÇÇ : L‚ÇÅ ‚ü∂ L‚ÇÇ`. -/
  h‚ÇÉ : IsColimit (CokernelCofork.ofœÄ _ w‚ÇÅ‚ÇÉ)
  L‚ÇÅ_exact  : L‚ÇÅ.Exact
  epi_L‚ÇÅ_g : Epi L‚ÇÅ.g
  L‚ÇÇ_exact : L‚ÇÇ.Exact
  mono_L‚ÇÇ_f : Mono L‚ÇÇ.f

initialize_simps_projections SnakeInput (-h‚ÇÄ, -h‚ÇÉ)

namespace SnakeInput

attribute [reassoc (attr := simp)] w‚ÇÄ‚ÇÇ w‚ÇÅ‚ÇÉ
attribute [instance] epi_L‚ÇÅ_g
attribute [instance] mono_L‚ÇÇ_f

variable {C}
variable (S : SnakeInput C)

/-- The snake input in the opposite category that is deduced from a snake input. -/
@[simps]
noncomputable def op : SnakeInput C·µí·µñ where
  L‚ÇÄ := S.L‚ÇÉ.op
  L‚ÇÅ := S.L‚ÇÇ.op
  L‚ÇÇ := S.L‚ÇÅ.op
  L‚ÇÉ := S.L‚ÇÄ.op
  epi_L‚ÇÅ_g := by dsimp; infer_instance
  mono_L‚ÇÇ_f := by dsimp; infer_instance
  v‚ÇÄ‚ÇÅ := opMap S.v‚ÇÇ‚ÇÉ
  v‚ÇÅ‚ÇÇ := opMap S.v‚ÇÅ‚ÇÇ
  v‚ÇÇ‚ÇÉ := opMap S.v‚ÇÄ‚ÇÅ
  w‚ÇÄ‚ÇÇ := congr_arg opMap S.w‚ÇÅ‚ÇÉ
  w‚ÇÅ‚ÇÉ := congr_arg opMap S.w‚ÇÄ‚ÇÇ
  h‚ÇÄ := isLimitForkMapOfIsLimit' (ShortComplex.opEquiv C).functor _
      (CokernelCofork.IsColimit.ofœÄOp _ _ S.h‚ÇÉ)
  h‚ÇÉ := isColimitCoforkMapOfIsColimit' (ShortComplex.opEquiv C).functor _
      (KernelFork.IsLimit.ofŒπOp _ _ S.h‚ÇÄ)
  L‚ÇÅ_exact := S.L‚ÇÇ_exact.op
  L‚ÇÇ_exact := S.L‚ÇÅ_exact.op

@[reassoc (attr := simp)] lemma w‚ÇÄ‚ÇÇ_œÑ‚ÇÅ : S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÅ ‚â´ S.v‚ÇÅ‚ÇÇ.œÑ‚ÇÅ = 0 := by
  rw [‚Üê comp_œÑ‚ÇÅ, S.w‚ÇÄ‚ÇÇ, zero_œÑ‚ÇÅ]
@[reassoc (attr := simp)] lemma w‚ÇÄ‚ÇÇ_œÑ‚ÇÇ : S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÇ ‚â´ S.v‚ÇÅ‚ÇÇ.œÑ‚ÇÇ = 0 := by
  rw [‚Üê comp_œÑ‚ÇÇ, S.w‚ÇÄ‚ÇÇ, zero_œÑ‚ÇÇ]
@[reassoc (attr := simp)] lemma w‚ÇÄ‚ÇÇ_œÑ‚ÇÉ : S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÉ ‚â´ S.v‚ÇÅ‚ÇÇ.œÑ‚ÇÉ = 0 := by
  rw [‚Üê comp_œÑ‚ÇÉ, S.w‚ÇÄ‚ÇÇ, zero_œÑ‚ÇÉ]
@[reassoc (attr := simp)] lemma w‚ÇÅ‚ÇÉ_œÑ‚ÇÅ : S.v‚ÇÅ‚ÇÇ.œÑ‚ÇÅ ‚â´ S.v‚ÇÇ‚ÇÉ.œÑ‚ÇÅ = 0 := by
  rw [‚Üê comp_œÑ‚ÇÅ, S.w‚ÇÅ‚ÇÉ, zero_œÑ‚ÇÅ]
@[reassoc (attr := simp)] lemma w‚ÇÅ‚ÇÉ_œÑ‚ÇÇ : S.v‚ÇÅ‚ÇÇ.œÑ‚ÇÇ ‚â´ S.v‚ÇÇ‚ÇÉ.œÑ‚ÇÇ = 0 := by
  rw [‚Üê comp_œÑ‚ÇÇ, S.w‚ÇÅ‚ÇÉ, zero_œÑ‚ÇÇ]
@[reassoc (attr := simp)] lemma w‚ÇÅ‚ÇÉ_œÑ‚ÇÉ : S.v‚ÇÅ‚ÇÇ.œÑ‚ÇÉ ‚â´ S.v‚ÇÇ‚ÇÉ.œÑ‚ÇÉ = 0 := by
  rw [‚Üê comp_œÑ‚ÇÉ, S.w‚ÇÅ‚ÇÉ, zero_œÑ‚ÇÉ]

/-- `L‚ÇÄ.X‚ÇÅ` is the kernel of `v‚ÇÅ‚ÇÇ.œÑ‚ÇÅ : L‚ÇÅ.X‚ÇÅ ‚ü∂ L‚ÇÇ.X‚ÇÅ`. -/
noncomputable def h‚ÇÄœÑ‚ÇÅ : IsLimit (KernelFork.ofŒπ S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÅ S.w‚ÇÄ‚ÇÇ_œÑ‚ÇÅ) :=
  isLimitForkMapOfIsLimit' œÄ‚ÇÅ S.w‚ÇÄ‚ÇÇ S.h‚ÇÄ

/-- `L‚ÇÄ.X‚ÇÇ` is the kernel of `v‚ÇÅ‚ÇÇ.œÑ‚ÇÇ : L‚ÇÅ.X‚ÇÇ ‚ü∂ L‚ÇÇ.X‚ÇÇ`. -/
noncomputable def h‚ÇÄœÑ‚ÇÇ : IsLimit (KernelFork.ofŒπ S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÇ S.w‚ÇÄ‚ÇÇ_œÑ‚ÇÇ) :=
  isLimitForkMapOfIsLimit' œÄ‚ÇÇ S.w‚ÇÄ‚ÇÇ S.h‚ÇÄ

/-- `L‚ÇÄ.X‚ÇÉ` is the kernel of `v‚ÇÅ‚ÇÇ.œÑ‚ÇÉ : L‚ÇÅ.X‚ÇÉ ‚ü∂ L‚ÇÇ.X‚ÇÉ`. -/
noncomputable def h‚ÇÄœÑ‚ÇÉ : IsLimit (KernelFork.ofŒπ S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÉ S.w‚ÇÄ‚ÇÇ_œÑ‚ÇÉ) :=
  isLimitForkMapOfIsLimit' œÄ‚ÇÉ S.w‚ÇÄ‚ÇÇ S.h‚ÇÄ

instance mono_v‚ÇÄ‚ÇÅ_œÑ‚ÇÅ : Mono S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÅ := mono_of_isLimit_fork S.h‚ÇÄœÑ‚ÇÅ
instance mono_v‚ÇÄ‚ÇÅ_œÑ‚ÇÇ : Mono S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÇ := mono_of_isLimit_fork S.h‚ÇÄœÑ‚ÇÇ
instance mono_v‚ÇÄ‚ÇÅ_œÑ‚ÇÉ : Mono S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÉ := mono_of_isLimit_fork S.h‚ÇÄœÑ‚ÇÉ

/-- The upper part of the first column of the snake diagram is exact. -/
lemma exact_C‚ÇÅ_up : (ShortComplex.mk S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÅ S.v‚ÇÅ‚ÇÇ.œÑ‚ÇÅ
    (by rw [‚Üê comp_œÑ‚ÇÅ, S.w‚ÇÄ‚ÇÇ, zero_œÑ‚ÇÅ])).Exact :=
  exact_of_f_is_kernel _ S.h‚ÇÄœÑ‚ÇÅ

/-- The upper part of the second column of the snake diagram is exact. -/
lemma exact_C‚ÇÇ_up : (ShortComplex.mk S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÇ S.v‚ÇÅ‚ÇÇ.œÑ‚ÇÇ
    (by rw [‚Üê comp_œÑ‚ÇÇ, S.w‚ÇÄ‚ÇÇ, zero_œÑ‚ÇÇ])).Exact :=
  exact_of_f_is_kernel _ S.h‚ÇÄœÑ‚ÇÇ

/-- The upper part of the third column of the snake diagram is exact. -/
lemma exact_C‚ÇÉ_up : (ShortComplex.mk S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÉ S.v‚ÇÅ‚ÇÇ.œÑ‚ÇÉ
    (by rw [‚Üê comp_œÑ‚ÇÉ, S.w‚ÇÄ‚ÇÇ, zero_œÑ‚ÇÉ])).Exact :=
  exact_of_f_is_kernel _ S.h‚ÇÄœÑ‚ÇÉ

instance mono_L‚ÇÄ_f [Mono S.L‚ÇÅ.f] : Mono S.L‚ÇÄ.f := by
  have : Mono (S.L‚ÇÄ.f ‚â´ S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÇ) := by
    rw [‚Üê S.v‚ÇÄ‚ÇÅ.comm‚ÇÅ‚ÇÇ]
    apply mono_comp
  exact mono_of_mono _ S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÇ

/-- `L‚ÇÉ.X‚ÇÅ` is the cokernel of `v‚ÇÅ‚ÇÇ.œÑ‚ÇÅ : L‚ÇÅ.X‚ÇÅ ‚ü∂ L‚ÇÇ.X‚ÇÅ`. -/
noncomputable def h‚ÇÉœÑ‚ÇÅ : IsColimit (CokernelCofork.ofœÄ S.v‚ÇÇ‚ÇÉ.œÑ‚ÇÅ S.w‚ÇÅ‚ÇÉ_œÑ‚ÇÅ) :=
  isColimitCoforkMapOfIsColimit' œÄ‚ÇÅ S.w‚ÇÅ‚ÇÉ S.h‚ÇÉ

/-- `L‚ÇÉ.X‚ÇÇ` is the cokernel of `v‚ÇÅ‚ÇÇ.œÑ‚ÇÇ : L‚ÇÅ.X‚ÇÇ ‚ü∂ L‚ÇÇ.X‚ÇÇ`. -/
noncomputable def h‚ÇÉœÑ‚ÇÇ : IsColimit (CokernelCofork.ofœÄ S.v‚ÇÇ‚ÇÉ.œÑ‚ÇÇ S.w‚ÇÅ‚ÇÉ_œÑ‚ÇÇ) :=
  isColimitCoforkMapOfIsColimit' œÄ‚ÇÇ S.w‚ÇÅ‚ÇÉ S.h‚ÇÉ

/-- `L‚ÇÉ.X‚ÇÉ` is the cokernel of `v‚ÇÅ‚ÇÇ.œÑ‚ÇÉ : L‚ÇÅ.X‚ÇÉ ‚ü∂ L‚ÇÇ.X‚ÇÉ`. -/
noncomputable def h‚ÇÉœÑ‚ÇÉ : IsColimit (CokernelCofork.ofœÄ S.v‚ÇÇ‚ÇÉ.œÑ‚ÇÉ S.w‚ÇÅ‚ÇÉ_œÑ‚ÇÉ) :=
  isColimitCoforkMapOfIsColimit' œÄ‚ÇÉ S.w‚ÇÅ‚ÇÉ S.h‚ÇÉ

instance epi_v‚ÇÇ‚ÇÉ_œÑ‚ÇÅ : Epi S.v‚ÇÇ‚ÇÉ.œÑ‚ÇÅ := epi_of_isColimit_cofork S.h‚ÇÉœÑ‚ÇÅ
instance epi_v‚ÇÇ‚ÇÉ_œÑ‚ÇÇ : Epi S.v‚ÇÇ‚ÇÉ.œÑ‚ÇÇ := epi_of_isColimit_cofork S.h‚ÇÉœÑ‚ÇÇ
instance epi_v‚ÇÇ‚ÇÉ_œÑ‚ÇÉ : Epi S.v‚ÇÇ‚ÇÉ.œÑ‚ÇÉ := epi_of_isColimit_cofork S.h‚ÇÉœÑ‚ÇÉ

/-- The lower part of the first column of the snake diagram is exact. -/
lemma exact_C‚ÇÅ_down: (ShortComplex.mk S.v‚ÇÅ‚ÇÇ.œÑ‚ÇÅ S.v‚ÇÇ‚ÇÉ.œÑ‚ÇÅ
    (by rw [‚Üê comp_œÑ‚ÇÅ, S.w‚ÇÅ‚ÇÉ, zero_œÑ‚ÇÅ])).Exact :=
  exact_of_g_is_cokernel _ S.h‚ÇÉœÑ‚ÇÅ

/-- The lower part of the second column of the snake diagram is exact. -/
lemma exact_C‚ÇÇ_down : (ShortComplex.mk S.v‚ÇÅ‚ÇÇ.œÑ‚ÇÇ S.v‚ÇÇ‚ÇÉ.œÑ‚ÇÇ
    (by rw [‚Üê comp_œÑ‚ÇÇ, S.w‚ÇÅ‚ÇÉ, zero_œÑ‚ÇÇ])).Exact :=
  exact_of_g_is_cokernel _ S.h‚ÇÉœÑ‚ÇÇ

/-- The lower part of the third column of the snake diagram is exact. -/
lemma exact_C‚ÇÉ_down : (ShortComplex.mk S.v‚ÇÅ‚ÇÇ.œÑ‚ÇÉ S.v‚ÇÇ‚ÇÉ.œÑ‚ÇÉ
    (by rw [‚Üê comp_œÑ‚ÇÉ, S.w‚ÇÅ‚ÇÉ, zero_œÑ‚ÇÉ])).Exact :=
  exact_of_g_is_cokernel _ S.h‚ÇÉœÑ‚ÇÉ

instance epi_L‚ÇÉ_g [Epi S.L‚ÇÇ.g] : Epi S.L‚ÇÉ.g := by
  have : Epi (S.v‚ÇÇ‚ÇÉ.œÑ‚ÇÇ ‚â´ S.L‚ÇÉ.g) := by
    rw [S.v‚ÇÇ‚ÇÉ.comm‚ÇÇ‚ÇÉ]
    apply epi_comp
  exact epi_of_epi S.v‚ÇÇ‚ÇÉ.œÑ‚ÇÇ _

lemma L‚ÇÄ_exact : S.L‚ÇÄ.Exact := by
  rw [ShortComplex.exact_iff_exact_up_to_refinements]
  intro A x‚ÇÇ hx‚ÇÇ
  obtain ‚ü®A‚ÇÅ, œÄ‚ÇÅ, hœÄ‚ÇÅ, y‚ÇÅ, hy‚ÇÅ‚ü© := S.L‚ÇÅ_exact.exact_up_to_refinements (x‚ÇÇ ‚â´ S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÇ)
    (by rw [assoc, S.v‚ÇÄ‚ÇÅ.comm‚ÇÇ‚ÇÉ, reassoc_of% hx‚ÇÇ, zero_comp])
  have hy‚ÇÅ' : y‚ÇÅ ‚â´ S.v‚ÇÅ‚ÇÇ.œÑ‚ÇÅ = 0 := by
    simp only [‚Üê cancel_mono S.L‚ÇÇ.f, assoc, zero_comp, S.v‚ÇÅ‚ÇÇ.comm‚ÇÅ‚ÇÇ,
      ‚Üê reassoc_of% hy‚ÇÅ, w‚ÇÄ‚ÇÇ_œÑ‚ÇÇ, comp_zero]
  obtain ‚ü®x‚ÇÅ, hx‚ÇÅ‚ü© : ‚àÉ x‚ÇÅ, x‚ÇÅ ‚â´ S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÅ = y‚ÇÅ := ‚ü®_, S.exact_C‚ÇÅ_up.lift_f y‚ÇÅ hy‚ÇÅ'‚ü©
  refine ‚ü®A‚ÇÅ, œÄ‚ÇÅ, hœÄ‚ÇÅ, x‚ÇÅ, ?_‚ü©
  simp only [‚Üê cancel_mono S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÇ, assoc, ‚Üê S.v‚ÇÄ‚ÇÅ.comm‚ÇÅ‚ÇÇ, reassoc_of% hx‚ÇÅ, hy‚ÇÅ]

lemma L‚ÇÉ_exact : S.L‚ÇÉ.Exact := S.op.L‚ÇÄ_exact.unop

/-- The fiber product of `L‚ÇÅ.X‚ÇÇ` and `L‚ÇÄ.X‚ÇÉ` over `L‚ÇÅ.X‚ÇÉ`. This is an auxiliary
object in the construction of the morphism `Œ¥ : L‚ÇÄ.X‚ÇÉ ‚ü∂ L‚ÇÉ.X‚ÇÅ`. -/
noncomputable def P := pullback S.L‚ÇÅ.g S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÉ

/-- The canonical map `P ‚ü∂ L‚ÇÇ.X‚ÇÇ`. -/
noncomputable def œÜ‚ÇÇ : S.P ‚ü∂ S.L‚ÇÇ.X‚ÇÇ := pullback.fst ‚â´ S.v‚ÇÅ‚ÇÇ.œÑ‚ÇÇ

@[reassoc (attr := simp)]
lemma lift_œÜ‚ÇÇ {A : C} (a : A ‚ü∂ S.L‚ÇÅ.X‚ÇÇ) (b : A ‚ü∂ S.L‚ÇÄ.X‚ÇÉ) (h : a ‚â´ S.L‚ÇÅ.g = b ‚â´ S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÉ) :
    pullback.lift a b h ‚â´ S.œÜ‚ÇÇ = a ‚â´ S.v‚ÇÅ‚ÇÇ.œÑ‚ÇÇ := by
  simp [œÜ‚ÇÇ]

/-- The canonical map `P ‚ü∂ L‚ÇÇ.X‚ÇÅ`. -/
noncomputable def œÜ‚ÇÅ : S.P ‚ü∂ S.L‚ÇÇ.X‚ÇÅ :=
  S.L‚ÇÇ_exact.lift S.œÜ‚ÇÇ
    (by simp only [œÜ‚ÇÇ, assoc, S.v‚ÇÅ‚ÇÇ.comm‚ÇÇ‚ÇÉ, pullback.condition_assoc, w‚ÇÄ‚ÇÇ_œÑ‚ÇÉ, comp_zero])

@[reassoc (attr := simp)] lemma œÜ‚ÇÅ_L‚ÇÇ_f : S.œÜ‚ÇÅ ‚â´ S.L‚ÇÇ.f = S.œÜ‚ÇÇ := S.L‚ÇÇ_exact.lift_f _ _

/-- The short complex that is part of an exact sequence `L‚ÇÅ.X‚ÇÅ ‚ü∂ P ‚ü∂ L‚ÇÄ.X‚ÇÉ ‚ü∂ 0`. -/
noncomputable def L‚ÇÄ' : ShortComplex C where
  X‚ÇÅ := S.L‚ÇÅ.X‚ÇÅ
  X‚ÇÇ := S.P
  X‚ÇÉ := S.L‚ÇÄ.X‚ÇÉ
  f := pullback.lift S.L‚ÇÅ.f 0 (by simp)
  g := pullback.snd
  zero := by simp

@[reassoc (attr := simp)] lemma L‚ÇÅ_f_œÜ‚ÇÅ : S.L‚ÇÄ'.f ‚â´ S.œÜ‚ÇÅ = S.v‚ÇÅ‚ÇÇ.œÑ‚ÇÅ := by
  dsimp only [L‚ÇÄ']
  simp only [‚Üê cancel_mono S.L‚ÇÇ.f, assoc, œÜ‚ÇÅ_L‚ÇÇ_f, œÜ‚ÇÇ, pullback.lift_fst_assoc,
    S.v‚ÇÅ‚ÇÇ.comm‚ÇÅ‚ÇÇ]

instance : Epi S.L‚ÇÄ'.g := by dsimp only [L‚ÇÄ']; infer_instance

instance [Mono S.L‚ÇÅ.f] : Mono S.L‚ÇÄ'.f :=
  mono_of_mono_fac (show S.L‚ÇÄ'.f ‚â´ pullback.fst = S.L‚ÇÅ.f by simp [L‚ÇÄ'])

lemma L‚ÇÄ'_exact : S.L‚ÇÄ'.Exact := by
  rw [ShortComplex.exact_iff_exact_up_to_refinements]
  intro A x‚ÇÇ hx‚ÇÇ
  dsimp [L‚ÇÄ'] at x‚ÇÇ hx‚ÇÇ
  obtain ‚ü®A', œÄ, hœÄ, x‚ÇÅ, fac‚ü© := S.L‚ÇÅ_exact.exact_up_to_refinements (x‚ÇÇ ‚â´ pullback.fst)
    (by rw [assoc, pullback.condition, reassoc_of% hx‚ÇÇ, zero_comp])
  exact ‚ü®A', œÄ, hœÄ, x‚ÇÅ, pullback.hom_ext (by simpa [L‚ÇÄ'] using fac) (by simp [L‚ÇÄ', hx‚ÇÇ])‚ü©

/-- The connecting homomorphism `Œ¥ : L‚ÇÄ.X‚ÇÉ ‚ü∂ L‚ÇÉ.X‚ÇÅ`. -/
noncomputable def Œ¥ : S.L‚ÇÄ.X‚ÇÉ ‚ü∂ S.L‚ÇÉ.X‚ÇÅ :=
  S.L‚ÇÄ'_exact.desc (S.œÜ‚ÇÅ ‚â´ S.v‚ÇÇ‚ÇÉ.œÑ‚ÇÅ) (by simp only [L‚ÇÅ_f_œÜ‚ÇÅ_assoc, w‚ÇÅ‚ÇÉ_œÑ‚ÇÅ])

@[reassoc (attr := simp)]
lemma snd_Œ¥ : (pullback.snd : S.P ‚ü∂ _) ‚â´ S.Œ¥ = S.œÜ‚ÇÅ ‚â´ S.v‚ÇÇ‚ÇÉ.œÑ‚ÇÅ :=
  S.L‚ÇÄ'_exact.g_desc _ _

/-- The pushout of `L‚ÇÇ.X‚ÇÇ` and `L‚ÇÉ.X‚ÇÅ` along `L‚ÇÇ.X‚ÇÅ`. -/
noncomputable def P' := pushout S.L‚ÇÇ.f S.v‚ÇÇ‚ÇÉ.œÑ‚ÇÅ

lemma snd_Œ¥_inr : (pullback.snd : S.P ‚ü∂ _) ‚â´ S.Œ¥ ‚â´ (pushout.inr : _ ‚ü∂ S.P') =
    pullback.fst ‚â´ S.v‚ÇÅ‚ÇÇ.œÑ‚ÇÇ ‚â´ pushout.inl := by
  simp only [snd_Œ¥_assoc, ‚Üê pushout.condition, œÜ‚ÇÇ, œÜ‚ÇÅ_L‚ÇÇ_f_assoc, assoc]

/-- The canonical morphism `L‚ÇÄ.X‚ÇÇ ‚ü∂ P`. -/
@[simp]
noncomputable def L‚ÇÄX‚ÇÇToP : S.L‚ÇÄ.X‚ÇÇ ‚ü∂ S.P := pullback.lift S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÇ S.L‚ÇÄ.g S.v‚ÇÄ‚ÇÅ.comm‚ÇÇ‚ÇÉ

@[reassoc]
lemma L‚ÇÄX‚ÇÇToP_comp_pullback_snd : S.L‚ÇÄX‚ÇÇToP ‚â´ pullback.snd = S.L‚ÇÄ.g := by simp

@[reassoc]
lemma L‚ÇÄX‚ÇÇToP_comp_œÜ‚ÇÅ : S.L‚ÇÄX‚ÇÇToP ‚â´ S.œÜ‚ÇÅ = 0 := by
  simp only [‚Üê cancel_mono S.L‚ÇÇ.f, L‚ÇÄX‚ÇÇToP, assoc, œÜ‚ÇÇ, œÜ‚ÇÅ_L‚ÇÇ_f,
    pullback.lift_fst_assoc, w‚ÇÄ‚ÇÇ_œÑ‚ÇÇ, zero_comp]

lemma L‚ÇÄ_g_Œ¥ : S.L‚ÇÄ.g ‚â´ S.Œ¥ = 0 := by
  erw [‚Üê L‚ÇÄX‚ÇÇToP_comp_pullback_snd, assoc, S.L‚ÇÄ'_exact.g_desc,
    L‚ÇÄX‚ÇÇToP_comp_œÜ‚ÇÅ_assoc, zero_comp]

lemma Œ¥_L‚ÇÉ_f : S.Œ¥ ‚â´ S.L‚ÇÉ.f = 0 := by
  erw [‚Üê cancel_epi S.L‚ÇÄ'.g, S.L‚ÇÄ'_exact.g_desc_assoc, assoc, S.v‚ÇÇ‚ÇÉ.comm‚ÇÅ‚ÇÇ, S.œÜ‚ÇÅ_L‚ÇÇ_f_assoc,
    œÜ‚ÇÇ, assoc, w‚ÇÅ‚ÇÉ_œÑ‚ÇÇ, comp_zero, comp_zero]

/-- The short complex `L‚ÇÄ.X‚ÇÇ ‚ü∂ L‚ÇÄ.X‚ÇÉ ‚ü∂ L‚ÇÉ.X‚ÇÅ`. -/
@[simps]
noncomputable def L‚ÇÅ' : ShortComplex C := ShortComplex.mk _ _ S.L‚ÇÄ_g_Œ¥

/-- The short complex `L‚ÇÄ.X‚ÇÉ ‚ü∂ L‚ÇÉ.X‚ÇÅ ‚ü∂ L‚ÇÉ.X‚ÇÇ`. -/
@[simps]
noncomputable def L‚ÇÇ' : ShortComplex C := ShortComplex.mk _ _ S.Œ¥_L‚ÇÉ_f

/-- Exactness of `L‚ÇÄ.X‚ÇÇ ‚ü∂ L‚ÇÄ.X‚ÇÉ ‚ü∂ L‚ÇÉ.X‚ÇÅ`. -/
lemma L‚ÇÅ'_exact : S.L‚ÇÅ'.Exact := by
  rw [ShortComplex.exact_iff_exact_up_to_refinements]
  intro A‚ÇÄ x‚ÇÉ hx‚ÇÉ
  dsimp at x‚ÇÉ hx‚ÇÉ
  obtain ‚ü®A‚ÇÅ, œÄ‚ÇÅ, hœÄ‚ÇÅ, p, hp‚ü© := surjective_up_to_refinements_of_epi S.L‚ÇÄ'.g x‚ÇÉ
  dsimp [L‚ÇÄ'] at p hp
  have hp' : (p ‚â´ S.œÜ‚ÇÅ) ‚â´ S.v‚ÇÇ‚ÇÉ.œÑ‚ÇÅ = 0 := by
    rw [assoc, ‚Üê S.snd_Œ¥, ‚Üê reassoc_of% hp, hx‚ÇÉ, comp_zero]
  obtain ‚ü®A‚ÇÇ, œÄ‚ÇÇ, hœÄ‚ÇÇ, x‚ÇÅ, hx‚ÇÅ‚ü© := S.exact_C‚ÇÅ_down.exact_up_to_refinements (p ‚â´ S.œÜ‚ÇÅ) hp'
  dsimp at x‚ÇÅ hx‚ÇÅ
  let x‚ÇÇ' := x‚ÇÅ ‚â´ S.L‚ÇÅ.f
  let x‚ÇÇ := œÄ‚ÇÇ ‚â´ p ‚â´ pullback.fst
  have hx‚ÇÇ' : (x‚ÇÇ - x‚ÇÇ') ‚â´ S.v‚ÇÅ‚ÇÇ.œÑ‚ÇÇ = 0 := by
    simp only [x‚ÇÇ, x‚ÇÇ', sub_comp, assoc, ‚Üê S.v‚ÇÅ‚ÇÇ.comm‚ÇÅ‚ÇÇ, ‚Üê reassoc_of% hx‚ÇÅ, œÜ‚ÇÇ, œÜ‚ÇÅ_L‚ÇÇ_f, sub_self]
  let k‚ÇÇ : A‚ÇÇ ‚ü∂ S.L‚ÇÄ.X‚ÇÇ := S.exact_C‚ÇÇ_up.lift _ hx‚ÇÇ'
  have hk‚ÇÇ : k‚ÇÇ ‚â´ S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÇ = x‚ÇÇ - x‚ÇÇ' := S.exact_C‚ÇÇ_up.lift_f _ _
  have hk‚ÇÇ' : k‚ÇÇ ‚â´ S.L‚ÇÄ.g = œÄ‚ÇÇ ‚â´ p ‚â´ pullback.snd := by
    simp only [x‚ÇÇ, x‚ÇÇ', ‚Üê cancel_mono S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÉ, assoc, ‚Üê S.v‚ÇÄ‚ÇÅ.comm‚ÇÇ‚ÇÉ, reassoc_of% hk‚ÇÇ,
      sub_comp, S.L‚ÇÅ.zero, comp_zero, sub_zero, pullback.condition]
  exact ‚ü®A‚ÇÇ, œÄ‚ÇÇ ‚â´ œÄ‚ÇÅ, epi_comp _ _, k‚ÇÇ, by simp only [assoc, L‚ÇÅ'_f, ‚Üê hk‚ÇÇ', hp]‚ü©

/-- The duality isomorphism `S.P ‚âÖ Opposite.unop S.op.P'`. -/
noncomputable def PIsoUnopOpP' : S.P ‚âÖ Opposite.unop S.op.P' := pullbackIsoUnopPushout _ _

/-- The duality isomorphism `S.P' ‚âÖ Opposite.unop S.op.P`. -/
noncomputable def P'IsoUnopOpP : S.P' ‚âÖ Opposite.unop S.op.P := pushoutIsoUnopPullback _ _

lemma op_Œ¥ : S.op.Œ¥ = S.Œ¥.op := Quiver.Hom.unop_inj (by
  rw [Quiver.Hom.unop_op, ‚Üê cancel_mono (pushout.inr : _ ‚ü∂ S.P'),
    ‚Üê cancel_epi (pullback.snd : S.P ‚ü∂ _), S.snd_Œ¥_inr,
    ‚Üê cancel_mono S.P'IsoUnopOpP.hom, ‚Üê cancel_epi S.PIsoUnopOpP'.inv,
    P'IsoUnopOpP, PIsoUnopOpP', assoc, assoc, assoc, assoc,
    pushoutIsoUnopPullback_inr_hom, pullbackIsoUnopPushout_inv_snd_assoc,
    pushoutIsoUnopPullback_inl_hom, pullbackIsoUnopPushout_inv_fst_assoc]
  apply Quiver.Hom.op_inj
  simpa only [op_comp, Quiver.Hom.op_unop, assoc] using S.op.snd_Œ¥_inr)

/-- The duality isomorphism `S.L‚ÇÇ'.op ‚âÖ S.op.L‚ÇÅ'`. -/
noncomputable def L‚ÇÇ'OpIso : S.L‚ÇÇ'.op ‚âÖ S.op.L‚ÇÅ' :=
  ShortComplex.isoMk (Iso.refl _) (Iso.refl _) (Iso.refl _) (by aesop_cat)
    (by dsimp; simp only [id_comp, comp_id, S.op_Œ¥])

/-- Exactness of `L‚ÇÄ.X‚ÇÉ ‚ü∂ L‚ÇÉ.X‚ÇÅ ‚ü∂ L‚ÇÉ.X‚ÇÇ`. -/
lemma L‚ÇÇ'_exact : S.L‚ÇÇ'.Exact := by
  rw [‚Üê exact_op_iff, exact_iff_of_iso S.L‚ÇÇ'OpIso]
  exact S.op.L‚ÇÅ'_exact

/-- The diagram `S.L‚ÇÄ.X‚ÇÅ ‚ü∂ S.L‚ÇÄ.X‚ÇÇ ‚ü∂ S.L‚ÇÄ.X‚ÇÉ ‚ü∂ S.L‚ÇÉ.X‚ÇÅ ‚ü∂ S.L‚ÇÉ.X‚ÇÇ ‚ü∂ S.L‚ÇÉ.X‚ÇÉ` for any
`S : SnakeInput C`. -/
noncomputable abbrev composableArrows : ComposableArrows C 5 :=
  ComposableArrows.mk‚ÇÖ S.L‚ÇÄ.f S.L‚ÇÄ.g S.Œ¥ S.L‚ÇÉ.f S.L‚ÇÉ.g

open ComposableArrows in
/-- The diagram `S.L‚ÇÄ.X‚ÇÅ ‚ü∂ S.L‚ÇÄ.X‚ÇÇ ‚ü∂ S.L‚ÇÄ.X‚ÇÉ ‚ü∂ S.L‚ÇÉ.X‚ÇÅ ‚ü∂ S.L‚ÇÉ.X‚ÇÇ ‚ü∂ S.L‚ÇÉ.X‚ÇÉ` is exact
for any `S : SnakeInput C`. -/
lemma snake_lemma : S.composableArrows.Exact :=
  exact_of_Œ¥‚ÇÄ S.L‚ÇÄ_exact.exact_toComposableArrows
    (exact_of_Œ¥‚ÇÄ S.L‚ÇÅ'_exact.exact_toComposableArrows
    (exact_of_Œ¥‚ÇÄ S.L‚ÇÇ'_exact.exact_toComposableArrows
    S.L‚ÇÉ_exact.exact_toComposableArrows))

lemma Œ¥_eq {A : C} (x‚ÇÉ : A ‚ü∂ S.L‚ÇÄ.X‚ÇÉ) (x‚ÇÇ : A ‚ü∂ S.L‚ÇÅ.X‚ÇÇ) (x‚ÇÅ : A ‚ü∂ S.L‚ÇÇ.X‚ÇÅ)
    (h‚ÇÇ : x‚ÇÇ ‚â´ S.L‚ÇÅ.g = x‚ÇÉ ‚â´ S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÉ) (h‚ÇÅ : x‚ÇÅ ‚â´ S.L‚ÇÇ.f = x‚ÇÇ ‚â´ S.v‚ÇÅ‚ÇÇ.œÑ‚ÇÇ) :
    x‚ÇÉ ‚â´ S.Œ¥ = x‚ÇÅ ‚â´ S.v‚ÇÇ‚ÇÉ.œÑ‚ÇÅ := by
  have H := (pullback.lift x‚ÇÇ x‚ÇÉ h‚ÇÇ) ‚â´= S.snd_Œ¥
  rw [pullback.lift_snd_assoc] at H
  rw [H, ‚Üê assoc]
  congr 1
  simp only [‚Üê cancel_mono S.L‚ÇÇ.f, assoc, œÜ‚ÇÅ_L‚ÇÇ_f, lift_œÜ‚ÇÇ, h‚ÇÅ]

variable (S‚ÇÅ S‚ÇÇ S‚ÇÉ : SnakeInput C)

/-- A morphism of snake inputs involve four morphisms of short complexes
which make the obvious diagram commute. -/
@[ext]
structure Hom :=
  /-- a morphism between the zeroth lines -/
  f‚ÇÄ : S‚ÇÅ.L‚ÇÄ ‚ü∂ S‚ÇÇ.L‚ÇÄ
  /-- a morphism between the first lines -/
  f‚ÇÅ : S‚ÇÅ.L‚ÇÅ ‚ü∂ S‚ÇÇ.L‚ÇÅ
  /-- a morphism between the second lines -/
  f‚ÇÇ : S‚ÇÅ.L‚ÇÇ ‚ü∂ S‚ÇÇ.L‚ÇÇ
  /-- a morphism between the third lines -/
  f‚ÇÉ : S‚ÇÅ.L‚ÇÉ ‚ü∂ S‚ÇÇ.L‚ÇÉ
  comm‚ÇÄ‚ÇÅ : f‚ÇÄ ‚â´ S‚ÇÇ.v‚ÇÄ‚ÇÅ = S‚ÇÅ.v‚ÇÄ‚ÇÅ ‚â´ f‚ÇÅ := by aesop_cat
  comm‚ÇÅ‚ÇÇ : f‚ÇÅ ‚â´ S‚ÇÇ.v‚ÇÅ‚ÇÇ = S‚ÇÅ.v‚ÇÅ‚ÇÇ ‚â´ f‚ÇÇ := by aesop_cat
  comm‚ÇÇ‚ÇÉ : f‚ÇÇ ‚â´ S‚ÇÇ.v‚ÇÇ‚ÇÉ = S‚ÇÅ.v‚ÇÇ‚ÇÉ ‚â´ f‚ÇÉ := by aesop_cat

namespace Hom

attribute [reassoc] comm‚ÇÄ‚ÇÅ comm‚ÇÅ‚ÇÇ comm‚ÇÇ‚ÇÉ

/-- The identity morphism of a snake input. -/
@[simps]
def id : Hom S S where
  f‚ÇÄ := ùüô _
  f‚ÇÅ := ùüô _
  f‚ÇÇ := ùüô _
  f‚ÇÉ := ùüô _

variable {S‚ÇÅ S‚ÇÇ S‚ÇÉ}

/-- The composition of morphisms of snake inputs. -/
@[simps]
def comp (f : Hom S‚ÇÅ S‚ÇÇ) (g : Hom S‚ÇÇ S‚ÇÉ) : Hom S‚ÇÅ S‚ÇÉ where
  f‚ÇÄ := f.f‚ÇÄ ‚â´ g.f‚ÇÄ
  f‚ÇÅ := f.f‚ÇÅ ‚â´ g.f‚ÇÅ
  f‚ÇÇ := f.f‚ÇÇ ‚â´ g.f‚ÇÇ
  f‚ÇÉ := f.f‚ÇÉ ‚â´ g.f‚ÇÉ
  comm‚ÇÄ‚ÇÅ := by simp only [assoc, comm‚ÇÄ‚ÇÅ, comm‚ÇÄ‚ÇÅ_assoc]
  comm‚ÇÅ‚ÇÇ := by simp only [assoc, comm‚ÇÅ‚ÇÇ, comm‚ÇÅ‚ÇÇ_assoc]
  comm‚ÇÇ‚ÇÉ := by simp only [assoc, comm‚ÇÇ‚ÇÉ, comm‚ÇÇ‚ÇÉ_assoc]

end Hom

instance : Category (SnakeInput C) where
  Hom := Hom
  id := Hom.id
  comp := Hom.comp

variable {S‚ÇÅ S‚ÇÇ S‚ÇÉ}

@[simp] lemma id_f‚ÇÄ : Hom.f‚ÇÄ (ùüô S) = ùüô _ := rfl
@[simp] lemma id_f‚ÇÅ : Hom.f‚ÇÅ (ùüô S) = ùüô _ := rfl
@[simp] lemma id_f‚ÇÇ : Hom.f‚ÇÇ (ùüô S) = ùüô _ := rfl
@[simp] lemma id_f‚ÇÉ : Hom.f‚ÇÉ (ùüô S) = ùüô _ := rfl

section

variable (f : S‚ÇÅ ‚ü∂ S‚ÇÇ) (g : S‚ÇÇ ‚ü∂ S‚ÇÉ)

@[simp, reassoc] lemma comp_f‚ÇÄ : (f ‚â´ g).f‚ÇÄ = f.f‚ÇÄ ‚â´ g.f‚ÇÄ := rfl
@[simp, reassoc] lemma comp_f‚ÇÅ : (f ‚â´ g).f‚ÇÅ = f.f‚ÇÅ ‚â´ g.f‚ÇÅ := rfl
@[simp, reassoc] lemma comp_f‚ÇÇ : (f ‚â´ g).f‚ÇÇ = f.f‚ÇÇ ‚â´ g.f‚ÇÇ := rfl
@[simp, reassoc] lemma comp_f‚ÇÉ : (f ‚â´ g).f‚ÇÉ = f.f‚ÇÉ ‚â´ g.f‚ÇÉ := rfl

end

/-- The functor which sends `S : SnakeInput C` to its zeroth line `S.L‚ÇÄ`. -/
@[simps]
def functorL‚ÇÄ : SnakeInput C ‚•§ ShortComplex C where
  obj S := S.L‚ÇÄ
  map f := f.f‚ÇÄ

/-- The functor which sends `S : SnakeInput C` to its zeroth line `S.L‚ÇÅ`. -/
@[simps]
def functorL‚ÇÅ : SnakeInput C ‚•§ ShortComplex C where
  obj S := S.L‚ÇÅ
  map f := f.f‚ÇÅ

/-- The functor which sends `S : SnakeInput C` to its second line `S.L‚ÇÇ`. -/
@[simps]
def functorL‚ÇÇ : SnakeInput C ‚•§ ShortComplex C where
  obj S := S.L‚ÇÇ
  map f := f.f‚ÇÇ

/-- The functor which sends `S : SnakeInput C` to its third line `S.L‚ÇÉ`. -/
@[simps]
def functorL‚ÇÉ : SnakeInput C ‚•§ ShortComplex C where
  obj S := S.L‚ÇÉ
  map f := f.f‚ÇÉ

/-- The functor which sends `S : SnakeInput C` to the auxiliary object `S.P`,
which is `pullback S.L‚ÇÅ.g S.v‚ÇÄ‚ÇÅ.œÑ‚ÇÉ`. -/
@[simps]
noncomputable def functorP : SnakeInput C ‚•§ C where
  obj S := S.P
  map f := pullback.map _ _ _ _ f.f‚ÇÅ.œÑ‚ÇÇ f.f‚ÇÄ.œÑ‚ÇÉ f.f‚ÇÅ.œÑ‚ÇÉ f.f‚ÇÅ.comm‚ÇÇ‚ÇÉ.symm
      (congr_arg ShortComplex.Hom.œÑ‚ÇÉ f.comm‚ÇÄ‚ÇÅ.symm)
  map_id _ := by dsimp [P]; aesop_cat
  map_comp _ _ := by dsimp [P]; aesop_cat

@[reassoc]
lemma naturality_œÜ‚ÇÇ (f : S‚ÇÅ ‚ü∂ S‚ÇÇ) : S‚ÇÅ.œÜ‚ÇÇ ‚â´ f.f‚ÇÇ.œÑ‚ÇÇ = functorP.map f ‚â´ S‚ÇÇ.œÜ‚ÇÇ := by
  dsimp [œÜ‚ÇÇ]
  simp only [assoc, pullback.lift_fst_assoc, ‚Üê comp_œÑ‚ÇÇ, f.comm‚ÇÅ‚ÇÇ]

@[reassoc]
lemma naturality_œÜ‚ÇÅ (f : S‚ÇÅ ‚ü∂ S‚ÇÇ) : S‚ÇÅ.œÜ‚ÇÅ ‚â´ f.f‚ÇÇ.œÑ‚ÇÅ = functorP.map f ‚â´ S‚ÇÇ.œÜ‚ÇÅ := by
  simp only [‚Üê cancel_mono S‚ÇÇ.L‚ÇÇ.f, assoc, œÜ‚ÇÅ_L‚ÇÇ_f, ‚Üê naturality_œÜ‚ÇÇ, f.f‚ÇÇ.comm‚ÇÅ‚ÇÇ, œÜ‚ÇÅ_L‚ÇÇ_f_assoc]

@[reassoc]
lemma naturality_Œ¥ (f : S‚ÇÅ ‚ü∂ S‚ÇÇ) : S‚ÇÅ.Œ¥ ‚â´ f.f‚ÇÉ.œÑ‚ÇÅ = f.f‚ÇÄ.œÑ‚ÇÉ ‚â´ S‚ÇÇ.Œ¥ := by
  rw [‚Üê cancel_epi (pullback.snd : S‚ÇÅ.P ‚ü∂ _), S‚ÇÅ.snd_Œ¥_assoc, ‚Üê comp_œÑ‚ÇÅ, ‚Üê f.comm‚ÇÇ‚ÇÉ,
    comp_œÑ‚ÇÅ, naturality_œÜ‚ÇÅ_assoc, ‚Üê S‚ÇÇ.snd_Œ¥, functorP_map, pullback.lift_snd_assoc, assoc]

/-- The functor which sends `S : SnakeInput C` to `S.L‚ÇÅ'` which is
`S.L‚ÇÄ.X‚ÇÇ ‚ü∂ S.L‚ÇÄ.X‚ÇÉ ‚ü∂ S.L‚ÇÉ.X‚ÇÅ`. -/
@[simps]
noncomputable def functorL‚ÇÅ' : SnakeInput C ‚•§ ShortComplex C where
  obj S := S.L‚ÇÅ'
  map f :=
    { œÑ‚ÇÅ := f.f‚ÇÄ.œÑ‚ÇÇ
      œÑ‚ÇÇ := f.f‚ÇÄ.œÑ‚ÇÉ
      œÑ‚ÇÉ := f.f‚ÇÉ.œÑ‚ÇÅ
      comm‚ÇÅ‚ÇÇ := f.f‚ÇÄ.comm‚ÇÇ‚ÇÉ
      comm‚ÇÇ‚ÇÉ := (naturality_Œ¥ f).symm }

/-- The functor which sends `S : SnakeInput C` to `S.L‚ÇÇ'` which is
`S.L‚ÇÄ.X‚ÇÉ ‚ü∂ S.L‚ÇÉ.X‚ÇÅ ‚ü∂ S.L‚ÇÉ.X‚ÇÇ`. -/
@[simps]
noncomputable def functorL‚ÇÇ' : SnakeInput C ‚•§ ShortComplex C where
  obj S := S.L‚ÇÇ'
  map f :=
    { œÑ‚ÇÅ := f.f‚ÇÄ.œÑ‚ÇÉ
      œÑ‚ÇÇ := f.f‚ÇÉ.œÑ‚ÇÅ
      œÑ‚ÇÉ := f.f‚ÇÉ.œÑ‚ÇÇ
      comm‚ÇÅ‚ÇÇ := (naturality_Œ¥ f).symm
      comm‚ÇÇ‚ÇÉ := f.f‚ÇÉ.comm‚ÇÅ‚ÇÇ }

/-- The functor which maps `S : SnakeInput C` to the diagram
`S.L‚ÇÄ.X‚ÇÅ ‚ü∂ S.L‚ÇÄ.X‚ÇÇ ‚ü∂ S.L‚ÇÄ.X‚ÇÉ ‚ü∂ S.L‚ÇÉ.X‚ÇÅ ‚ü∂ S.L‚ÇÉ.X‚ÇÇ ‚ü∂ S.L‚ÇÉ.X‚ÇÉ`. -/
@[simps]
noncomputable def composableArrowsFunctor : SnakeInput C ‚•§ ComposableArrows C 5 where
  obj S := S.composableArrows
  map f := ComposableArrows.homMk‚ÇÖ f.f‚ÇÄ.œÑ‚ÇÅ f.f‚ÇÄ.œÑ‚ÇÇ f.f‚ÇÄ.œÑ‚ÇÉ f.f‚ÇÉ.œÑ‚ÇÅ f.f‚ÇÉ.œÑ‚ÇÇ f.f‚ÇÉ.œÑ‚ÇÉ
    f.f‚ÇÄ.comm‚ÇÅ‚ÇÇ.symm f.f‚ÇÄ.comm‚ÇÇ‚ÇÉ.symm (naturality_Œ¥ f) f.f‚ÇÉ.comm‚ÇÅ‚ÇÇ.symm f.f‚ÇÉ.comm‚ÇÇ‚ÇÉ.symm

end SnakeInput

end ShortComplex

end CategoryTheory
