/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import Mathlib.Algebra.Homology.ShortComplex.Basic
import Mathlib.CategoryTheory.Limits.Shapes.Kernels

/-! LeftHomology of short complexes

Given a short complex `S : ShortComplex C`, which consists of two composable
maps `f : Xâ‚ âŸ¶ Xâ‚‚` and `g : Xâ‚‚ âŸ¶ Xâ‚ƒ` such that `f â‰« g = 0`, we shall define
here the "left homology" `S.leftHomology` of `S` (TODO). For this, we introduce the
notion of "left homology data". Such an `h : S.LeftHomologyData` consists of the
datum of morphisms `i : K âŸ¶ Xâ‚‚` and `Ï€ : K âŸ¶ H` such that `i` identifies
`K` to the kernel of `g : Xâ‚‚ âŸ¶ Xâ‚ƒ`, and that `Ï€` identifies `H` to the cokernel
of the induced map `f' : Xâ‚ âŸ¶ K`.

When such a `S.LeftHomologyData` exists, we shall say that `[S.HasLeftHomology]`
and we define `S.leftHomology` to be the `H` field of a chosen left homology data.
Similarly, we define `S.cycles` to be the `K` field.

The dual notion is defined in `RightHomologyData.lean`. In `Homology.lean`,
when `S` has two compatible left and right homology data (i.e. they give
the same `H` up to a canonical isomorphism), we shall define `[S.HasHomology]`
and `S.homology` (TODO).

-/

namespace CategoryTheory

open Category Limits

namespace ShortComplex

variable {C : Type _} [Category C] [HasZeroMorphisms C] (S : ShortComplex C)
  {Sâ‚ Sâ‚‚ Sâ‚ƒ : ShortComplex C}

/-- A left homology data for a short complex `S` consists of morphisms `i : K âŸ¶ S.Xâ‚‚` and
`Ï€ : K âŸ¶ H` such that `i` identifies `K` to the kernel of `g : S.Xâ‚‚ âŸ¶ S.Xâ‚ƒ`,
and that `Ï€` identifies `H` to the cokernel of the induced map `f' : S.Xâ‚ âŸ¶ K` --/
structure LeftHomologyData where
  /-- a choice of kernel of `S.g : S.Xâ‚‚ âŸ¶ S.Xâ‚ƒ`-/
  K : C
  /-- a choice of cokernel of the induced morphism `S.f' : S.Xâ‚ âŸ¶ K`-/
  H : C
  /-- the inclusion of cycles in `S.Xâ‚‚` -/
  i : K âŸ¶ S.Xâ‚‚
  /-- the projection from cycles to the (left) homology -/
  Ï€ : K âŸ¶ H
  /-- the kernel condition for `i` -/
  wi : i â‰« S.g = 0
  /-- `i : K âŸ¶ S.Xâ‚‚ ` is a kernel of `g : S.Xâ‚‚ âŸ¶ S.Xâ‚ƒ` -/
  hi : IsLimit (KernelFork.ofÎ¹ i wi)
  /-- the cokernel condition for `Ï€` -/
  wÏ€ : hi.lift (KernelFork.ofÎ¹ _ S.zero) â‰« Ï€ = 0
  /-- `Ï€ : K âŸ¶ H ` is a cokernel of the induced morphism `S.f' : S.Xâ‚ âŸ¶ K` -/
  hÏ€ : IsColimit (CokernelCofork.ofÏ€ Ï€ wÏ€)

initialize_simps_projections LeftHomologyData (-hi, -hÏ€)

namespace LeftHomologyData

/-- The chosen kernels and cokernels of the limits API give a `LeftHomologyData` -/
@[simps]
noncomputable def ofHasKernelOfHasCokernel
    [HasKernel S.g] [HasCokernel (kernel.lift S.g S.f S.zero)] :
  S.LeftHomologyData where
  K := kernel S.g
  H := cokernel (kernel.lift S.g S.f S.zero)
  i := kernel.Î¹ _
  Ï€ := cokernel.Ï€ _
  wi := kernel.condition _
  hi := kernelIsKernel _
  wÏ€ := cokernel.condition _
  hÏ€ := cokernelIsCokernel _

attribute [reassoc (attr := simp)] wi wÏ€

variable {S}
variable (h : S.LeftHomologyData) {A : C}

instance : Mono h.i := âŸ¨fun _ _ => Fork.IsLimit.hom_ext h.hiâŸ©

instance : Epi h.Ï€ := âŸ¨fun _ _ => Cofork.IsColimit.hom_ext h.hÏ€âŸ©

/-- Any morphism `k : A âŸ¶ S.Xâ‚‚` that is a cycle (i.e. `k â‰« S.g = 0`) lifts
to a morphism `A âŸ¶ K` -/
def liftK (k : A âŸ¶ S.Xâ‚‚) (hk : k â‰« S.g = 0) : A âŸ¶ h.K := h.hi.lift (KernelFork.ofÎ¹ k hk)

@[reassoc (attr := simp)]
lemma liftK_i (k : A âŸ¶ S.Xâ‚‚) (hk : k â‰« S.g = 0) : h.liftK k hk â‰« h.i = k :=
  h.hi.fac _ WalkingParallelPair.zero

/-- The (left) homology class `A âŸ¶ H` attached to a cycle `k : A âŸ¶ S.Xâ‚‚` -/
@[simp]
def liftH (k : A âŸ¶ S.Xâ‚‚) (hk : k â‰« S.g = 0) : A âŸ¶ h.H := h.liftK k hk â‰« h.Ï€

/-- Given `h : LeftHomologyData S`, this is morphism `S.Xâ‚ âŸ¶ h.K` induced
by `S.f : S.Xâ‚ âŸ¶ S.Xâ‚‚` and the fact that `h.K` is a kernel of `S.g : S.Xâ‚‚ âŸ¶ S.Xâ‚ƒ`. -/
def f' : S.Xâ‚ âŸ¶ h.K := h.liftK S.f S.zero

@[reassoc (attr := simp)] lemma f'_i : h.f' â‰« h.i = S.f := liftK_i _ _ _

@[reassoc (attr := simp)] lemma f'_Ï€ : h.f' â‰« h.Ï€ = 0 := h.wÏ€

@[reassoc]
lemma liftK_Ï€_eq_zero_of_boundary (k : A âŸ¶ S.Xâ‚‚) (x : A âŸ¶ S.Xâ‚) (hx : k = x â‰« S.f) :
    h.liftK k (by rw [hx, assoc, S.zero, comp_zero]) â‰« h.Ï€ = 0 := by
  rw [show 0 = (x â‰« h.f') â‰« h.Ï€ by simp]
  congr 1
  simp only [â† cancel_mono h.i, hx, liftK_i, assoc, f'_i]

/-- For `h : S.LeftHomologyData`, this is a restatement of `h.hÏ€`, saying that
`Ï€ : h.K âŸ¶ h.H` is a cokernel of `h.f' : S.Xâ‚ âŸ¶ h.K`. -/
def hÏ€' : IsColimit (CokernelCofork.ofÏ€ h.Ï€ h.f'_Ï€) := h.hÏ€

/-- The morphism `H âŸ¶ A` induced by a morphism `k : K âŸ¶ A` such that `f' â‰« k = 0` -/
def descH (k : h.K âŸ¶ A) (hk : h.f' â‰« k = 0) : h.H âŸ¶ A :=
  h.hÏ€.desc (CokernelCofork.ofÏ€ k hk)

@[reassoc (attr := simp)]
lemma Ï€_descH (k : h.K âŸ¶ A) (hk : h.f' â‰« k = 0) : h.Ï€ â‰« h.descH k hk = k :=
  h.hÏ€.fac (CokernelCofork.ofÏ€ k hk) WalkingParallelPair.one

variable (S)

/-- When the second map `S.g` is zero, this is the left homology data on `S` given
by any colimit cokernel cofork of `S.f` -/
@[simps]
def ofIsColimitCokernelCofork (hg : S.g = 0) (c : CokernelCofork S.f) (hc : IsColimit c) :
    S.LeftHomologyData where
  K := S.Xâ‚‚
  H := c.pt
  i := ğŸ™ _
  Ï€ := c.Ï€
  wi := by rw [id_comp, hg]
  hi := KernelFork.IsLimit.ofId _ hg
  wÏ€ := CokernelCofork.condition _
  hÏ€ := IsColimit.ofIsoColimit hc (Cofork.ext (Iso.refl _) (by aesop_cat))

@[simp] lemma ofIsColimitCokernelCofork_f' (hg : S.g = 0) (c : CokernelCofork S.f)
    (hc : IsColimit c) : (ofIsColimitCokernelCofork S hg c hc).f' = S.f := by
  rw [â† cancel_mono (ofIsColimitCokernelCofork S hg c hc).i, f'_i,
    ofIsColimitCokernelCofork_i]
  dsimp
  rw [comp_id]

/-- When the second map `S.g` is zero, this is the left homology data on `S` given by
the chosen `cokernel S.f` -/
@[simps!]
noncomputable def ofHasCokernel [HasCokernel S.f] (hg : S.g = 0) : S.LeftHomologyData :=
  ofIsColimitCokernelCofork S hg _ (cokernelIsCokernel _)

/-- When the first map `S.f` is zero, this is the left homology data on `S` given
by any limit kernel fork of `S.g` -/
@[simps]
def ofIsLimitKernelFork (hf : S.f = 0) (c : KernelFork S.g) (hc : IsLimit c) :
    S.LeftHomologyData where
  K := c.pt
  H := c.pt
  i := c.Î¹
  Ï€ := ğŸ™ _
  wi := KernelFork.condition _
  hi := IsLimit.ofIsoLimit hc (Fork.ext (Iso.refl _) (by aesop_cat))
  wÏ€ := Fork.IsLimit.hom_ext hc (by
    dsimp
    simp only [comp_id, zero_comp, Fork.IsLimit.lift_Î¹, Fork.Î¹_ofÎ¹, hf])
  hÏ€ := CokernelCofork.IsColimit.ofId _ (Fork.IsLimit.hom_ext hc (by
    dsimp
    simp only [comp_id, zero_comp, Fork.IsLimit.lift_Î¹, Fork.Î¹_ofÎ¹, hf]))

@[simp] lemma ofIsLimitKernelFork_f' (hf : S.f = 0) (c : KernelFork S.g) (hc : IsLimit c) :
    (ofIsLimitKernelFork S hf c hc).f' = 0 := by
  rw [â† cancel_mono (ofIsLimitKernelFork S hf c hc).i, f'_i, hf, zero_comp]

/-- When the first map `S.f` is zero, this is the left homology data on `S` given
by the chosen `kernel S.g` -/
@[simp]
noncomputable def ofHasKernel [HasKernel S.g] (hf : S.f = 0) : S.LeftHomologyData :=
  ofIsLimitKernelFork S hf _ (kernelIsKernel _)

/-- When both `S.f` and `S.g` are zero, the middle object `S.Xâ‚‚` gives a left homology data on S -/
@[simps]
def ofZeros (hf : S.f = 0) (hg : S.g = 0) : S.LeftHomologyData where
  K := S.Xâ‚‚
  H := S.Xâ‚‚
  i := ğŸ™ _
  Ï€ := ğŸ™ _
  wi := by rw [id_comp, hg]
  hi := KernelFork.IsLimit.ofId _ hg
  wÏ€ := by
    change S.f â‰« ğŸ™ _ = 0
    simp only [hf, zero_comp]
  hÏ€ := CokernelCofork.IsColimit.ofId _ hf

@[simp] lemma ofZeros_f' (hf : S.f = 0) (hg : S.g = 0) :
    (ofZeros S hf hg).f' = 0 := by
  rw [â† cancel_mono ((ofZeros S hf hg).i), zero_comp, f'_i, hf]

end LeftHomologyData

/-- A short complex `S` has left homology when there exists a `S.LeftHomologyData` -/
class HasLeftHomology : Prop where
  condition : Nonempty S.LeftHomologyData

/-- A chosen `S.LeftHomologyData` for a short complex `S` that has left homology -/
noncomputable def leftHomologyData [S.HasLeftHomology] :
  S.LeftHomologyData := HasLeftHomology.condition.some

variable {S}

namespace HasLeftHomology

lemma mk' (h : S.LeftHomologyData) : HasLeftHomology S := âŸ¨Nonempty.intro hâŸ©

instance of_hasKernel_of_hasCokernel [HasKernel S.g] [HasCokernel (kernel.lift S.g S.f S.zero)] :
  S.HasLeftHomology := HasLeftHomology.mk' (LeftHomologyData.ofHasKernelOfHasCokernel S)

instance of_hasCokernel {X Y : C} (f : X âŸ¶ Y) (Z : C) [HasCokernel f] :
    (ShortComplex.mk f (0 : Y âŸ¶ Z) comp_zero).HasLeftHomology :=
  HasLeftHomology.mk' (LeftHomologyData.ofHasCokernel _ rfl)

instance of_hasKernel {Y Z : C} (g : Y âŸ¶ Z) (X : C) [HasKernel g] :
    (ShortComplex.mk (0 : X âŸ¶ Y) g zero_comp).HasLeftHomology :=
  HasLeftHomology.mk' (LeftHomologyData.ofHasKernel _ rfl)

instance of_zeros (X Y Z : C) :
    (ShortComplex.mk (0 : X âŸ¶ Y) (0 : Y âŸ¶ Z) zero_comp).HasLeftHomology :=
  HasLeftHomology.mk' (LeftHomologyData.ofZeros _ rfl rfl)

end HasLeftHomology

section

variable (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (hâ‚ : Sâ‚.LeftHomologyData) (hâ‚‚ : Sâ‚‚.LeftHomologyData)

/-- Given left homology data `hâ‚` and `hâ‚‚` for two short complexes `Sâ‚` and `Sâ‚‚`,
a `LeftHomologyMapData` for a morphism `Ï† : Sâ‚ âŸ¶ Sâ‚‚`
consists of a description of the induced morphisms on the `K` (cycles)
and `H` (left homology) fields of `hâ‚` and `hâ‚‚`. -/
structure LeftHomologyMapData where
  /-- the induced map on cycles -/
  Ï†K : hâ‚.K âŸ¶ hâ‚‚.K
  /-- the induced map on left homology -/
  Ï†H : hâ‚.H âŸ¶ hâ‚‚.H
  /-- commutation with `i` -/
  commi : Ï†K â‰« hâ‚‚.i = hâ‚.i â‰« Ï†.Ï„â‚‚ := by aesop_cat
  /-- commutation with `f'` -/
  commf' : hâ‚.f' â‰« Ï†K = Ï†.Ï„â‚ â‰« hâ‚‚.f' := by aesop_cat
  /-- commutation with `Ï€` -/
  commÏ€ : hâ‚.Ï€ â‰« Ï†H = Ï†K â‰« hâ‚‚.Ï€ := by aesop_cat

namespace LeftHomologyMapData

attribute [reassoc (attr := simp)] commi commf' commÏ€
attribute [nolint simpNF] mk.injEq

/-- The left homology map data associated to the zero morphism between two short complexes. -/
@[simps]
def zero (hâ‚ : Sâ‚.LeftHomologyData) (hâ‚‚ : Sâ‚‚.LeftHomologyData) :
    LeftHomologyMapData 0 hâ‚ hâ‚‚ where
  Ï†K := 0
  Ï†H := 0

/-- The left homology map data associated to the identity morphism of a short complex. -/
@[simps]
def id (h : S.LeftHomologyData) : LeftHomologyMapData (ğŸ™ S) h h where
  Ï†K := ğŸ™ _
  Ï†H := ğŸ™ _

/-- The composition of left homology map data. -/
@[simps]
def comp {Ï† : Sâ‚ âŸ¶ Sâ‚‚} {Ï†' : Sâ‚‚ âŸ¶ Sâ‚ƒ}
    {hâ‚ : Sâ‚.LeftHomologyData} {hâ‚‚ : Sâ‚‚.LeftHomologyData} {hâ‚ƒ : Sâ‚ƒ.LeftHomologyData}
    (Ïˆ : LeftHomologyMapData Ï† hâ‚ hâ‚‚) (Ïˆ' : LeftHomologyMapData Ï†' hâ‚‚ hâ‚ƒ) :
    LeftHomologyMapData (Ï† â‰« Ï†') hâ‚ hâ‚ƒ where
  Ï†K := Ïˆ.Ï†K â‰« Ïˆ'.Ï†K
  Ï†H := Ïˆ.Ï†H â‰« Ïˆ'.Ï†H

instance : Subsingleton (LeftHomologyMapData Ï† hâ‚ hâ‚‚) :=
  âŸ¨fun Ïˆâ‚ Ïˆâ‚‚ => by
    have hK : Ïˆâ‚.Ï†K = Ïˆâ‚‚.Ï†K := by rw [â† cancel_mono hâ‚‚.i, commi, commi]
    have hH : Ïˆâ‚.Ï†H = Ïˆâ‚‚.Ï†H := by rw [â† cancel_epi hâ‚.Ï€, commÏ€, commÏ€, hK]
    cases Ïˆâ‚
    cases Ïˆâ‚‚
    congrâŸ©

instance : Inhabited (LeftHomologyMapData Ï† hâ‚ hâ‚‚) := âŸ¨by
  let Ï†K : hâ‚.K âŸ¶ hâ‚‚.K := hâ‚‚.liftK (hâ‚.i â‰« Ï†.Ï„â‚‚)
    (by rw [assoc, Ï†.commâ‚‚â‚ƒ, hâ‚.wi_assoc, zero_comp])
  have commf' : hâ‚.f' â‰« Ï†K = Ï†.Ï„â‚ â‰« hâ‚‚.f' := by
    rw [â† cancel_mono hâ‚‚.i, assoc, assoc, LeftHomologyData.liftK_i,
      LeftHomologyData.f'_i_assoc, LeftHomologyData.f'_i, Ï†.commâ‚â‚‚]
  let Ï†H : hâ‚.H âŸ¶ hâ‚‚.H := hâ‚.descH (Ï†K â‰« hâ‚‚.Ï€)
    (by rw [reassoc_of% commf', hâ‚‚.f'_Ï€, comp_zero])
  exact âŸ¨Ï†K, Ï†H, by simp, commf', by simpâŸ©âŸ©

instance : Unique (LeftHomologyMapData Ï† hâ‚ hâ‚‚) := Unique.mk' _

variable {Ï† hâ‚ hâ‚‚}

lemma congr_Ï†H {Î³â‚ Î³â‚‚ : LeftHomologyMapData Ï† hâ‚ hâ‚‚} (eq : Î³â‚ = Î³â‚‚) : Î³â‚.Ï†H = Î³â‚‚.Ï†H := by rw [eq]
lemma congr_Ï†K {Î³â‚ Î³â‚‚ : LeftHomologyMapData Ï† hâ‚ hâ‚‚} (eq : Î³â‚ = Î³â‚‚) : Î³â‚.Ï†K = Î³â‚‚.Ï†K := by rw [eq]

/-- When `Sâ‚.f`, `Sâ‚.g`, `Sâ‚‚.f` and `Sâ‚‚.g` are all zero, the action on left homology of a
morphism `Ï† : Sâ‚ âŸ¶ Sâ‚‚` is given by the action `Ï†.Ï„â‚‚` on the middle objects. -/
@[simps]
def ofZeros (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (hfâ‚ : Sâ‚.f = 0) (hgâ‚ : Sâ‚.g = 0) (hfâ‚‚ : Sâ‚‚.f = 0) (hgâ‚‚ : Sâ‚‚.g = 0) :
    LeftHomologyMapData Ï† (LeftHomologyData.ofZeros Sâ‚ hfâ‚ hgâ‚)
      (LeftHomologyData.ofZeros Sâ‚‚ hfâ‚‚ hgâ‚‚) where
  Ï†K := Ï†.Ï„â‚‚
  Ï†H := Ï†.Ï„â‚‚

/-- When `Sâ‚.g` and `Sâ‚‚.g` are zero and we have chosen colimit cokernel coforks `câ‚` and `câ‚‚`
for `Sâ‚.f` and `Sâ‚‚.f` respectively, the action on left homology of a morphism `Ï† : Sâ‚ âŸ¶ Sâ‚‚` of
short complexes is given by the unique morphism `f : câ‚.pt âŸ¶ câ‚‚.pt` such that
`Ï†.Ï„â‚‚ â‰« câ‚‚.Ï€ = câ‚.Ï€ â‰« f`. -/
@[simps]
def ofIsColimitCokernelCofork (Ï† : Sâ‚ âŸ¶ Sâ‚‚)
    (hgâ‚ : Sâ‚.g = 0) (câ‚ : CokernelCofork Sâ‚.f) (hcâ‚ : IsColimit câ‚)
    (hgâ‚‚ : Sâ‚‚.g = 0) (câ‚‚ : CokernelCofork Sâ‚‚.f) (hcâ‚‚ : IsColimit câ‚‚) (f : câ‚.pt âŸ¶ câ‚‚.pt)
    (comm : Ï†.Ï„â‚‚ â‰« câ‚‚.Ï€ = câ‚.Ï€ â‰« f) :
    LeftHomologyMapData Ï† (LeftHomologyData.ofIsColimitCokernelCofork Sâ‚ hgâ‚ câ‚ hcâ‚)
      (LeftHomologyData.ofIsColimitCokernelCofork Sâ‚‚ hgâ‚‚ câ‚‚ hcâ‚‚) where
  Ï†K := Ï†.Ï„â‚‚
  Ï†H := f
  commÏ€ := comm.symm
  commf' := by simp only [LeftHomologyData.ofIsColimitCokernelCofork_f', Ï†.commâ‚â‚‚]

/-- When `Sâ‚.f` and `Sâ‚‚.f` are zero and we have chosen limit kernel forks `câ‚` and `câ‚‚`
for `Sâ‚.g` and `Sâ‚‚.g` respectively, the action on left homology of a morphism `Ï† : Sâ‚ âŸ¶ Sâ‚‚` of
short complexes is given by the unique morphism `f : câ‚.pt âŸ¶ câ‚‚.pt` such that
`câ‚.Î¹ â‰« Ï†.Ï„â‚‚ = f â‰« câ‚‚.Î¹`. -/
@[simps]
def ofIsLimitKernelFork (Ï† : Sâ‚ âŸ¶ Sâ‚‚)
    (hfâ‚ : Sâ‚.f = 0) (câ‚ : KernelFork Sâ‚.g) (hcâ‚ : IsLimit câ‚)
    (hfâ‚‚ : Sâ‚‚.f = 0) (câ‚‚ : KernelFork Sâ‚‚.g) (hcâ‚‚ : IsLimit câ‚‚) (f : câ‚.pt âŸ¶ câ‚‚.pt)
    (comm : câ‚.Î¹ â‰« Ï†.Ï„â‚‚ = f â‰« câ‚‚.Î¹) :
    LeftHomologyMapData Ï† (LeftHomologyData.ofIsLimitKernelFork Sâ‚ hfâ‚ câ‚ hcâ‚)
      (LeftHomologyData.ofIsLimitKernelFork Sâ‚‚ hfâ‚‚ câ‚‚ hcâ‚‚) where
  Ï†K := f
  Ï†H := f
  commi := comm.symm

variable (S)

/-- When both maps `S.f` and `S.g` of a short complex `S` are zero, this is the homology map
data (for the identity of `S`) which relates the left homology data `ofZeros` and
`ofIsColimitCokernelCofork`. -/
@[simps]
def compatibilityOfZerosOfIsColimitCokernelCofork (hf : S.f = 0) (hg : S.g = 0)
    (c : CokernelCofork S.f) (hc : IsColimit c) :
    LeftHomologyMapData (ğŸ™ S) (LeftHomologyData.ofZeros S hf hg)
      (LeftHomologyData.ofIsColimitCokernelCofork S hg c hc) where
  Ï†K := ğŸ™ _
  Ï†H := c.Ï€

/-- When both maps `S.f` and `S.g` of a short complex `S` are zero, this is the homology map
data (for the identity of `S`) which relates the left homology data
`LeftHomologyData.ofIsLimitKernelFork` and `ofZeros` . -/
@[simps]
def compatibilityOfZerosOfIsLimitKernelFork (hf : S.f = 0) (hg : S.g = 0)
    (c : KernelFork S.g) (hc : IsLimit c) :
    LeftHomologyMapData (ğŸ™ S) (LeftHomologyData.ofIsLimitKernelFork S hf c hc)
      (LeftHomologyData.ofZeros S hf hg) where
  Ï†K := c.Î¹
  Ï†H := c.Î¹

end LeftHomologyMapData

end

section

variable (S)
variable [S.HasLeftHomology]

/-- The left homology of a short complex, given by the `H` field of a chosen left homology data.  -/
noncomputable def leftHomology : C := S.leftHomologyData.H

/-- The cycles of a short complex, given by the `K` field of a chosen left homology data.  -/
noncomputable def cycles : C := S.leftHomologyData.K

/-- The "homology class" map `S.cycles âŸ¶ S.leftHomology`. -/
noncomputable def leftHomologyÏ€ : S.cycles âŸ¶ S.leftHomology := S.leftHomologyData.Ï€

/-- The inclusion `S.cycles âŸ¶ S.Xâ‚‚`. -/
noncomputable def iCycles : S.cycles âŸ¶ S.Xâ‚‚ := S.leftHomologyData.i

/-- The "boundaries" map `S.Xâ‚ âŸ¶ S.cycles`. (Note that in this homology API, we make no use
of the "image" of this morphism, which under some categorical assumptions would be a subobject
of `S.Xâ‚‚` contained in `S.cycles`.) -/
noncomputable def toCycles : S.Xâ‚ âŸ¶ S.cycles := S.leftHomologyData.f'

@[reassoc (attr := simp)]
lemma iCycles_g : S.iCycles â‰« S.g = 0 := S.leftHomologyData.wi

@[reassoc (attr := simp)]
lemma toCycles_i : S.toCycles â‰« S.iCycles = S.f := S.leftHomologyData.f'_i

instance : Mono S.iCycles := by
  dsimp only [iCycles]
  infer_instance

instance : Epi S.leftHomologyÏ€ := by
  dsimp only [leftHomologyÏ€]
  infer_instance

end

section

variable (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (hâ‚ : Sâ‚.LeftHomologyData) (hâ‚‚ : Sâ‚‚.LeftHomologyData)

/-- The (unique) left homology map data associated to a morphism of short complexes that
are both equipped with left homology data. -/
def leftHomologyMapData : LeftHomologyMapData Ï† hâ‚ hâ‚‚ := default

/-- Given a morphism `Ï† : Sâ‚ âŸ¶ Sâ‚‚` of short complexes and left homology data `hâ‚` and `hâ‚‚`
for `Sâ‚` and `Sâ‚‚` respectively, this is the induced left homology map `hâ‚.H âŸ¶ hâ‚.H`. -/
def leftHomologyMap' : hâ‚.H âŸ¶ hâ‚‚.H := (leftHomologyMapData Ï† _ _).Ï†H

/-- Given a morphism `Ï† : Sâ‚ âŸ¶ Sâ‚‚` of short complexes and left homology data `hâ‚` and `hâ‚‚`
for `Sâ‚` and `Sâ‚‚` respectively, this is the induced morphism `hâ‚.K âŸ¶ hâ‚.K` on cycles. -/
def cyclesMap' : hâ‚.K âŸ¶ hâ‚‚.K := (leftHomologyMapData Ï† _ _).Ï†K

@[reassoc (attr := simp)]
lemma cyclesMap'_i : cyclesMap' Ï† hâ‚ hâ‚‚ â‰« hâ‚‚.i = hâ‚.i â‰« Ï†.Ï„â‚‚ :=
  LeftHomologyMapData.commi _

@[reassoc (attr := simp)]
lemma f'_cyclesMap' : hâ‚.f' â‰« cyclesMap' Ï† hâ‚ hâ‚‚ = Ï†.Ï„â‚ â‰« hâ‚‚.f' := by
  simp only [â† cancel_mono hâ‚‚.i, assoc, Ï†.commâ‚â‚‚, cyclesMap'_i,
    LeftHomologyData.f'_i_assoc, LeftHomologyData.f'_i]

@[reassoc (attr := simp)]
lemma leftHomologyÏ€_naturality' :
    hâ‚.Ï€ â‰« leftHomologyMap' Ï† hâ‚ hâ‚‚ = cyclesMap' Ï† hâ‚ hâ‚‚ â‰« hâ‚‚.Ï€ :=
  LeftHomologyMapData.commÏ€ _

end

section

variable [HasLeftHomology Sâ‚] [HasLeftHomology Sâ‚‚] (Ï† : Sâ‚ âŸ¶ Sâ‚‚)

/-- The (left) homology map `Sâ‚.leftHomology âŸ¶ Sâ‚‚.leftHomology` induced by a morphism
`Sâ‚ âŸ¶ Sâ‚‚` of short complexes. -/
noncomputable def leftHomologyMap : Sâ‚.leftHomology âŸ¶ Sâ‚‚.leftHomology :=
  leftHomologyMap' Ï† _ _

/-- The morphism `Sâ‚.cycles âŸ¶ Sâ‚‚.cycles` induced by a morphism `Sâ‚ âŸ¶ Sâ‚‚` of short complexes. -/
noncomputable def cyclesMap : Sâ‚.cycles âŸ¶ Sâ‚‚.cycles := cyclesMap' Ï† _ _

@[reassoc (attr := simp)]
lemma cyclesMap_i : cyclesMap Ï† â‰« Sâ‚‚.iCycles = Sâ‚.iCycles â‰« Ï†.Ï„â‚‚ :=
  cyclesMap'_i _ _ _

@[reassoc (attr := simp)]
lemma toCycles_naturality : Sâ‚.toCycles â‰« cyclesMap Ï† = Ï†.Ï„â‚ â‰« Sâ‚‚.toCycles :=
  f'_cyclesMap' _ _ _

@[reassoc (attr := simp)]
lemma leftHomologyÏ€_naturality :
    Sâ‚.leftHomologyÏ€ â‰« leftHomologyMap Ï† = cyclesMap Ï† â‰« Sâ‚‚.leftHomologyÏ€ :=
  leftHomologyÏ€_naturality' _ _ _

end

end ShortComplex

end CategoryTheory
