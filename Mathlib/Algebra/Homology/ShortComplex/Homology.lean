/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import Mathlib.Algebra.Homology.ShortComplex.RightHomology

/-! Homology of short complexes

In this file, we shall define the homology of short complexes `S`, i.e. diagrams
`f : Xâ‚ âŸ¶ Xâ‚‚` and `g : Xâ‚‚ âŸ¶ Xâ‚ƒ` such that `f â‰« g = 0`. We shall say that
`[S.HasHomology]` when there exists `h : S.HomologyData` (TODO). A homology data
for `S` consists of compatible left/right homology data `left` and `right`. The
left homology data `left` involves an object `left.H` that is a cokernel of the canonical
map `S.Xâ‚ âŸ¶ K` where `K` is a kernel of `g`. On the other hand, the dual notion `right.H`
is a kernel of the canonical morphism `Q âŸ¶ S.Xâ‚ƒ` when `Q` is a cokernel of `f`.
The compatibility that is required involves an isomorphism `left.H â‰… right.H` which
makes a certain pentagon commute.

This definition requires very little assumption on the category (only the existence
of zero morphisms). We shall prove that in abelian categories, all short complexes
have homology data.

Note: This definition arose by the end of the Liquid Tensor Experiment which
contained a structure `has_homology` which is quite similar to `S.HomologyData`.
After the category `ShortComplex C` was introduced by J. Riou, A. Topaz suggested
such a structure could be used as a basis for the *definition* of homology.

-/

namespace CategoryTheory

open Category Limits

variable {C D : Type _} [Category C] [Category D]
  [HasZeroMorphisms C]
  (S : ShortComplex C) {Sâ‚ Sâ‚‚ Sâ‚ƒ Sâ‚„ : ShortComplex C}

namespace ShortComplex

/-- A homology data for a short complex consists of two compatible left and
right homology data -/
structure HomologyData where
  left : S.LeftHomologyData
  right : S.RightHomologyData
  /-- the compatibility isomorphism relating the two dual notions of
    `LeftHomologyData` and `RightHomologyData`  -/
  iso : left.H â‰… right.H
  comm : left.Ï€ â‰« iso.hom â‰« right.Î¹ = left.i â‰« right.p := by aesop_cat

attribute [reassoc (attr := simp)] HomologyData.comm

variable (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (hâ‚ : Sâ‚.HomologyData) (hâ‚‚ : Sâ‚‚.HomologyData)

structure HomologyMapData :=
(left : LeftHomologyMapData Ï† hâ‚.left hâ‚‚.left)
(right : RightHomologyMapData Ï† hâ‚.right hâ‚‚.right)

namespace HomologyMapData

attribute [nolint simpNF] mk.injEq

variable {Ï† hâ‚ hâ‚‚}

@[reassoc]
lemma comm (h : HomologyMapData Ï† hâ‚ hâ‚‚) :
    h.left.Ï†H â‰« hâ‚‚.iso.hom = hâ‚.iso.hom â‰« h.right.Ï†H := by
  simp only [â† cancel_epi hâ‚.left.Ï€, â† cancel_mono hâ‚‚.right.Î¹, assoc,
    LeftHomologyMapData.commÏ€_assoc, HomologyData.comm, LeftHomologyMapData.commi_assoc,
    RightHomologyMapData.commÎ¹, HomologyData.comm_assoc, RightHomologyMapData.commp]

instance : Subsingleton (HomologyMapData Ï† hâ‚ hâ‚‚) := âŸ¨by
  rintro âŸ¨leftâ‚, rightâ‚âŸ© âŸ¨leftâ‚‚, rightâ‚‚âŸ©
  simp only [mk.injEq, eq_iff_true_of_subsingleton, and_self]âŸ©

instance : Inhabited (HomologyMapData Ï† hâ‚ hâ‚‚) :=
  âŸ¨âŸ¨default, defaultâŸ©âŸ©

instance : Unique (HomologyMapData Ï† hâ‚ hâ‚‚) := Unique.mk' _

variable (Ï† hâ‚ hâ‚‚)

def some : HomologyMapData Ï† hâ‚ hâ‚‚ := default

variable {Ï† hâ‚ hâ‚‚}

lemma congr_left_Ï†H {Î³â‚ Î³â‚‚ : HomologyMapData Ï† hâ‚ hâ‚‚} (eq : Î³â‚ = Î³â‚‚) :
  Î³â‚.left.Ï†H = Î³â‚‚.left.Ï†H := by rw [eq]

end HomologyMapData

namespace HomologyData

@[simps]
def ofIsLimitKernelFork (hf : S.f = 0) (c : KernelFork S.g) (hc : IsLimit c) :
    S.HomologyData where
  left := LeftHomologyData.ofIsLimitKernelFork S hf c hc
  right := RightHomologyData.ofIsLimitKernelFork S hf c hc
  iso := Iso.refl _

@[simps]
noncomputable def ofHasKernel (hf : S.f = 0) [HasKernel S.g] :
    S.HomologyData where
  left := LeftHomologyData.ofHasKernel S hf
  right := RightHomologyData.ofHasKernel S hf
  iso := Iso.refl _

@[simps]
def ofIsColimitCokernelCofork (hg : S.g = 0) (c : CokernelCofork S.f) (hc : IsColimit c) :
    S.HomologyData where
  left := LeftHomologyData.ofIsColimitCokernelCofork S hg c hc
  right := RightHomologyData.ofIsColimitCokernelCofork S hg c hc
  iso := Iso.refl _

@[simps]
noncomputable def ofHasCokernel (hg : S.g = 0) [HasCokernel S.f] :
    S.HomologyData where
  left := LeftHomologyData.ofHasCokernel S hg
  right := RightHomologyData.ofHasCokernel S hg
  iso := Iso.refl _

@[simps]
noncomputable def ofZeros (hf : S.f = 0) (hg : S.g = 0) :
    S.HomologyData where
  left := LeftHomologyData.ofZeros S hf hg
  right := RightHomologyData.ofZeros S hf hg
  iso := Iso.refl _

@[simps]
noncomputable def ofEpiOfIsIsoOfMono (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (h : HomologyData Sâ‚)
    [Epi Ï†.Ï„â‚] [IsIso Ï†.Ï„â‚‚] [Mono Ï†.Ï„â‚ƒ] : HomologyData Sâ‚‚ where
  left := LeftHomologyData.ofEpiOfIsIsoOfMono Ï† h.left
  right := RightHomologyData.ofEpiOfIsIsoOfMono Ï† h.right
  iso := h.iso

@[simps]
noncomputable def ofEpiOfIsIsoOfMono' (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (h : HomologyData Sâ‚‚)
    [Epi Ï†.Ï„â‚] [IsIso Ï†.Ï„â‚‚] [Mono Ï†.Ï„â‚ƒ] : HomologyData Sâ‚ where
  left := LeftHomologyData.ofEpiOfIsIsoOfMono' Ï† h.left
  right := RightHomologyData.ofEpiOfIsIsoOfMono' Ï† h.right
  iso := h.iso

@[simps!]
noncomputable def ofIso (e : Sâ‚ â‰… Sâ‚‚) (h : HomologyData Sâ‚) :=
  h.ofEpiOfIsIsoOfMono e.hom

variable {S}

@[simps]
def op (h : S.HomologyData) : S.op.HomologyData where
  left := h.right.op
  right := h.left.op
  iso := h.iso.op
  comm := Quiver.Hom.unop_inj (by simp)

@[simps]
def unop {S : ShortComplex Cáµ’áµ–} (h : S.HomologyData) : S.unop.HomologyData where
  left := h.right.unop
  right := h.left.unop
  iso := h.iso.unop
  comm := Quiver.Hom.op_inj (by simp)

end HomologyData

class HasHomology : Prop where
  condition : Nonempty S.HomologyData

noncomputable def homologyData [HasHomology S] :
  S.HomologyData := HasHomology.condition.some

variable {S}

lemma HasHomology.mk' (h : S.HomologyData) : HasHomology S :=
  âŸ¨Nonempty.intro hâŸ©

instance [HasHomology S] : HasHomology S.op :=
  HasHomology.mk' S.homologyData.op

instance hasLeftHomology_of_hasHomology [S.HasHomology] : S.HasLeftHomology :=
  HasLeftHomology.mk' S.homologyData.left

instance hasRightHomology_of_hasHomology [S.HasHomology] : S.HasRightHomology :=
  HasRightHomology.mk' S.homologyData.right

instance hasHomology_of_hasCokernel {X Y : C} (f : X âŸ¶ Y) (Z : C) [HasCokernel f] :
    (ShortComplex.mk f (0 : Y âŸ¶ Z) comp_zero).HasHomology :=
  HasHomology.mk' (HomologyData.ofHasCokernel _ rfl)

instance hasHomology_of_hasKernel {Y Z : C} (g : Y âŸ¶ Z) (X : C) [HasKernel g] :
    (ShortComplex.mk (0 : X âŸ¶ Y) g zero_comp).HasHomology :=
  HasHomology.mk' (HomologyData.ofHasKernel _ rfl)

instance hasHomology_of_zeros (X Y Z : C) :
    (ShortComplex.mk (0 : X âŸ¶ Y) (0 : Y âŸ¶ Z) zero_comp).HasHomology :=
  HasHomology.mk' (HomologyData.ofZeros _ rfl rfl)

lemma hasHomology_of_epi_of_isIso_of_mono (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [HasHomology Sâ‚]
    [Epi Ï†.Ï„â‚] [IsIso Ï†.Ï„â‚‚] [Mono Ï†.Ï„â‚ƒ] : HasHomology Sâ‚‚ :=
  HasHomology.mk' (HomologyData.ofEpiOfIsIsoOfMono Ï† Sâ‚.homologyData)

lemma hasHomology_of_epi_of_isIso_of_mono' (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [HasHomology Sâ‚‚]
    [Epi Ï†.Ï„â‚] [IsIso Ï†.Ï„â‚‚] [Mono Ï†.Ï„â‚ƒ] : HasHomology Sâ‚ :=
  HasHomology.mk' (HomologyData.ofEpiOfIsIsoOfMono' Ï† Sâ‚‚.homologyData)

lemma hasHomology_of_iso (e : Sâ‚ â‰… Sâ‚‚) [HasHomology Sâ‚] : HasHomology Sâ‚‚ :=
  HasHomology.mk' (HomologyData.ofIso e Sâ‚.homologyData)

namespace HomologyMapData

@[simps]
def id (h : S.HomologyData) : HomologyMapData (ğŸ™ S) h h where
  left := LeftHomologyMapData.id h.left
  right := RightHomologyMapData.id h.right

@[simps]
def zero (hâ‚ : Sâ‚.HomologyData) (hâ‚‚ : Sâ‚‚.HomologyData) :
  HomologyMapData 0 hâ‚ hâ‚‚ where
  left := LeftHomologyMapData.zero hâ‚.left hâ‚‚.left
  right := RightHomologyMapData.zero hâ‚.right hâ‚‚.right

@[simps]
def comp {Ï† : Sâ‚ âŸ¶ Sâ‚‚} {Ï†' : Sâ‚‚ âŸ¶ Sâ‚ƒ} {hâ‚ : Sâ‚.HomologyData}
    {hâ‚‚ : Sâ‚‚.HomologyData} {hâ‚ƒ : Sâ‚ƒ.HomologyData}
    (Ïˆ : HomologyMapData Ï† hâ‚ hâ‚‚) (Ïˆ' : HomologyMapData Ï†' hâ‚‚ hâ‚ƒ) :
    HomologyMapData (Ï† â‰« Ï†') hâ‚ hâ‚ƒ where
  left := Ïˆ.left.comp Ïˆ'.left
  right := Ïˆ.right.comp Ïˆ'.right

@[simps]
def op {Ï† : Sâ‚ âŸ¶ Sâ‚‚} {hâ‚ : Sâ‚.HomologyData} {hâ‚‚ : Sâ‚‚.HomologyData}
    (Ïˆ : HomologyMapData Ï† hâ‚ hâ‚‚) :
    HomologyMapData (opMap Ï†) hâ‚‚.op hâ‚.op where
  left := Ïˆ.right.op
  right := Ïˆ.left.op

@[simps]
def unop {Sâ‚ Sâ‚‚ : ShortComplex Cáµ’áµ–} {Ï† : Sâ‚ âŸ¶ Sâ‚‚}
    {hâ‚ : Sâ‚.HomologyData} {hâ‚‚ : Sâ‚‚.HomologyData}
    (Ïˆ : HomologyMapData Ï† hâ‚ hâ‚‚) :
    HomologyMapData (unopMap Ï†) hâ‚‚.unop hâ‚.unop where
  left := Ïˆ.right.unop
  right := Ïˆ.left.unop

@[simps]
def ofZeros (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (hfâ‚ : Sâ‚.f = 0) (hgâ‚ : Sâ‚.g = 0) (hfâ‚‚ : Sâ‚‚.f = 0) (hgâ‚‚ : Sâ‚‚.g = 0) :
    HomologyMapData Ï† (HomologyData.ofZeros Sâ‚ hfâ‚ hgâ‚) (HomologyData.ofZeros Sâ‚‚ hfâ‚‚ hgâ‚‚) where
  left := LeftHomologyMapData.ofZeros Ï† hfâ‚ hgâ‚ hfâ‚‚ hgâ‚‚
  right := RightHomologyMapData.ofZeros Ï† hfâ‚ hgâ‚ hfâ‚‚ hgâ‚‚

@[simps]
def ofIsColimitCokernelCofork (Ï† : Sâ‚ âŸ¶ Sâ‚‚)
  (hgâ‚ : Sâ‚.g = 0) (câ‚ : CokernelCofork Sâ‚.f) (hcâ‚ : IsColimit câ‚)
  (hgâ‚‚ : Sâ‚‚.g = 0) (câ‚‚ : CokernelCofork Sâ‚‚.f) (hcâ‚‚ : IsColimit câ‚‚) (f : câ‚.pt âŸ¶ câ‚‚.pt)
  (comm : Ï†.Ï„â‚‚ â‰« câ‚‚.Ï€ = câ‚.Ï€ â‰« f) :
  HomologyMapData Ï† (HomologyData.ofIsColimitCokernelCofork Sâ‚ hgâ‚ câ‚ hcâ‚)
    (HomologyData.ofIsColimitCokernelCofork Sâ‚‚ hgâ‚‚ câ‚‚ hcâ‚‚) where
  left := LeftHomologyMapData.ofIsColimitCokernelCofork Ï† hgâ‚ câ‚ hcâ‚ hgâ‚‚ câ‚‚ hcâ‚‚ f comm
  right := RightHomologyMapData.ofIsColimitCokernelCofork Ï† hgâ‚ câ‚ hcâ‚ hgâ‚‚ câ‚‚ hcâ‚‚ f comm

@[simps]
def ofIsLimitKernelFork (Ï† : Sâ‚ âŸ¶ Sâ‚‚)
  (hfâ‚ : Sâ‚.f = 0) (câ‚ : KernelFork Sâ‚.g) (hcâ‚ : IsLimit câ‚)
  (hfâ‚‚ : Sâ‚‚.f = 0) (câ‚‚ : KernelFork Sâ‚‚.g) (hcâ‚‚ : IsLimit câ‚‚) (f : câ‚.pt âŸ¶ câ‚‚.pt)
  (comm : câ‚.Î¹ â‰« Ï†.Ï„â‚‚ = f â‰« câ‚‚.Î¹) :
  HomologyMapData Ï† (HomologyData.ofIsLimitKernelFork Sâ‚ hfâ‚ câ‚ hcâ‚)
    (HomologyData.ofIsLimitKernelFork Sâ‚‚ hfâ‚‚ câ‚‚ hcâ‚‚) where
  left := LeftHomologyMapData.ofIsLimitKernelFork Ï† hfâ‚ câ‚ hcâ‚ hfâ‚‚ câ‚‚ hcâ‚‚ f comm
  right := RightHomologyMapData.ofIsLimitKernelFork Ï† hfâ‚ câ‚ hcâ‚ hfâ‚‚ câ‚‚ hcâ‚‚ f comm

def compatibilityOfZerosOfIsColimitCokernelCofork (hf : S.f = 0) (hg : S.g = 0)
  (c : CokernelCofork S.f) (hc : IsColimit c) :
  HomologyMapData (ğŸ™ S) (HomologyData.ofZeros S hf hg)
    (HomologyData.ofIsColimitCokernelCofork S hg c hc) where
  left := LeftHomologyMapData.compatibilityOfZerosOfIsColimitCokernelCofork S hf hg c hc
  right := RightHomologyMapData.compatibilityOfZerosOfIsColimitCokernelCofork S hf hg c hc

@[simps]
def compatibilityOfZerosOfIsLimitKernelFork (hf : S.f = 0) (hg : S.g = 0)
  (c : KernelFork S.g) (hc : IsLimit c) :
  HomologyMapData (ğŸ™ S)
    (HomologyData.ofIsLimitKernelFork S hf c hc)
    (HomologyData.ofZeros S hf hg) where
  left := LeftHomologyMapData.compatibilityOfZerosOfIsLimitKernelFork S hf hg c hc
  right := RightHomologyMapData.compatibilityOfZerosOfIsLimitKernelFork S hf hg c hc

noncomputable def ofEpiOfIsIsoOfMono (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (h : HomologyData Sâ‚)
    [Epi Ï†.Ï„â‚] [IsIso Ï†.Ï„â‚‚] [Mono Ï†.Ï„â‚ƒ] :
    HomologyMapData Ï† h (HomologyData.ofEpiOfIsIsoOfMono Ï† h) where
  left := LeftHomologyMapData.ofEpiOfIsIsoOfMono Ï† h.left
  right := RightHomologyMapData.ofEpiOfIsIsoOfMono Ï† h.right

noncomputable def ofEpiOfIsIsoOfMono' (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (h : HomologyData Sâ‚‚)
    [Epi Ï†.Ï„â‚] [IsIso Ï†.Ï„â‚‚] [Mono Ï†.Ï„â‚ƒ] :
    HomologyMapData Ï† (HomologyData.ofEpiOfIsIsoOfMono' Ï† h) h where
  left := LeftHomologyMapData.ofEpiOfIsIsoOfMono' Ï† h.left
  right := RightHomologyMapData.ofEpiOfIsIsoOfMono' Ï† h.right

end HomologyMapData

variable (S)

noncomputable def homology [HasHomology S] : C := S.homologyData.left.H

noncomputable def leftHomologyIso [S.HasHomology] : S.leftHomology â‰… S.homology :=
  leftHomologyMapIso' (Iso.refl _) _ _

noncomputable def rightHomologyIso [S.HasHomology] : S.rightHomology â‰… S.homology :=
  rightHomologyMapIso' (Iso.refl _) _ _ â‰ªâ‰« S.homologyData.iso.symm

variable {S}

noncomputable def LeftHomologyData.homologyIso (h : S.LeftHomologyData) [S.HasHomology] :
    S.homology â‰… h.H := S.leftHomologyIso.symm â‰ªâ‰« h.leftHomologyIso

noncomputable def RightHomologyData.homologyIso (h : S.RightHomologyData) [S.HasHomology] :
    S.homology â‰… h.H := S.rightHomologyIso.symm â‰ªâ‰« h.rightHomologyIso

variable (S)

@[simp]
lemma LeftHomologyData.homologyIso_leftHomologyData [S.HasHomology] :
    S.leftHomologyData.homologyIso = S.leftHomologyIso.symm := by
  ext
  dsimp only [homologyIso, Iso.symm, Iso.trans, leftHomologyIso, ShortComplex.leftHomologyIso,
    Iso.refl, leftHomologyMapIso']
  rw [â† leftHomologyMap'_comp, comp_id]

@[simp]
lemma RightHomologyData.homologyIso_rightHomologyData [S.HasHomology] :
    S.rightHomologyData.homologyIso = S.rightHomologyIso.symm := by
  ext
  dsimp only [homologyIso, Iso.symm, Iso.trans, Iso.refl, ShortComplex.rightHomologyIso,
    rightHomologyMapIso', rightHomologyIso]
  erw [rightHomologyMap'_id, comp_id]

variable {S}

def homologyMap' (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (hâ‚ : Sâ‚.HomologyData) (hâ‚‚ : Sâ‚‚.HomologyData) :
  hâ‚.left.H âŸ¶ hâ‚‚.left.H := leftHomologyMap' Ï† _ _

noncomputable def homologyMap (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [HasHomology Sâ‚] [HasHomology Sâ‚‚] :
    Sâ‚.homology âŸ¶ Sâ‚‚.homology :=
  homologyMap' Ï† _ _

namespace HomologyMapData

variable {Ï† : Sâ‚ âŸ¶ Sâ‚‚} {hâ‚ : Sâ‚.HomologyData} {hâ‚‚ : Sâ‚‚.HomologyData}
  (Î³ : HomologyMapData Ï† hâ‚ hâ‚‚)

lemma homologyMap'_eq : homologyMap' Ï† hâ‚ hâ‚‚ = Î³.left.Ï†H :=
  LeftHomologyMapData.congr_Ï†H (Subsingleton.elim _ _)

lemma cyclesMap'_eq : cyclesMap' Ï† hâ‚.left hâ‚‚.left = Î³.left.Ï†K :=
  LeftHomologyMapData.congr_Ï†K (Subsingleton.elim _ _)

lemma opcyclesMap'_eq : opcyclesMap' Ï† hâ‚.right hâ‚‚.right = Î³.right.Ï†Q :=
  RightHomologyMapData.congr_Ï†Q (Subsingleton.elim _ _)

end HomologyMapData

namespace LeftHomologyMapData

variable {hâ‚ : Sâ‚.LeftHomologyData} {hâ‚‚ : Sâ‚‚.LeftHomologyData}
  (Î³ : LeftHomologyMapData Ï† hâ‚ hâ‚‚) [Sâ‚.HasHomology] [Sâ‚‚.HasHomology]

lemma homologyMap_eq :
    homologyMap Ï† = hâ‚.homologyIso.hom â‰« Î³.Ï†H â‰« hâ‚‚.homologyIso.inv := by
  dsimp only [homologyMap, LeftHomologyData.homologyIso,
    Iso.trans, Iso.symm, leftHomologyIso, LeftHomologyData.leftHomologyIso,
    leftHomologyMapIso', homologyMap', Iso.refl]
  simp only [â† Î³.leftHomologyMap'_eq, â† leftHomologyMap'_comp, id_comp, comp_id]

lemma homologyMap_comm :
    homologyMap Ï† â‰« hâ‚‚.homologyIso.hom = hâ‚.homologyIso.hom â‰« Î³.Ï†H := by
  simp only [Î³.homologyMap_eq, assoc, Iso.inv_hom_id, comp_id]

end LeftHomologyMapData

namespace RightHomologyMapData

variable {hâ‚ : Sâ‚.RightHomologyData} {hâ‚‚ : Sâ‚‚.RightHomologyData}
  (Î³ : RightHomologyMapData Ï† hâ‚ hâ‚‚) [Sâ‚.HasHomology] [Sâ‚‚.HasHomology]

lemma homologyMap_eq :
    homologyMap Ï† = hâ‚.homologyIso.hom â‰« Î³.Ï†H â‰« hâ‚‚.homologyIso.inv := by
  dsimp only [homologyMap, homologyMap', RightHomologyData.homologyIso, Iso.symm, Iso.trans,
    Iso.refl, rightHomologyIso, rightHomologyMapIso', RightHomologyData.rightHomologyIso]
  have Î³' : HomologyMapData Ï† Sâ‚.homologyData Sâ‚‚.homologyData := default
  simp only [â† Î³.rightHomologyMap'_eq, assoc, â† rightHomologyMap'_comp_assoc,
    id_comp, comp_id, Î³'.left.leftHomologyMap'_eq, Î³'.right.rightHomologyMap'_eq, â† Î³'.comm_assoc,
    Iso.hom_inv_id]

lemma homologyMap_comm :
    homologyMap Ï† â‰« hâ‚‚.homologyIso.hom = hâ‚.homologyIso.hom â‰« Î³.Ï†H := by
  simp only [Î³.homologyMap_eq, assoc, Iso.inv_hom_id, comp_id]

end RightHomologyMapData

@[simp]
lemma homologyMap'_id (h : S.HomologyData) :
    homologyMap' (ğŸ™ S) h h = ğŸ™ _ :=
  (HomologyMapData.id h).homologyMap'_eq

variable (S)

@[simp]
lemma homologyMap_id [HasHomology S] :
    homologyMap (ğŸ™ S) = ğŸ™ _ :=
  homologyMap'_id _

@[simp]
lemma homologyMap'_zero (hâ‚ : Sâ‚.HomologyData) (hâ‚‚ : Sâ‚‚.HomologyData) :
    homologyMap' 0 hâ‚ hâ‚‚ = 0 :=
  (HomologyMapData.zero hâ‚ hâ‚‚).homologyMap'_eq

variable (Sâ‚ Sâ‚‚)

@[simp]
lemma homologyMap_zero [Sâ‚.HasHomology] [Sâ‚‚.HasHomology] :
    homologyMap (0 : Sâ‚ âŸ¶ Sâ‚‚) = 0 :=
  homologyMap'_zero _ _

variable {Sâ‚ Sâ‚‚}

lemma homologyMap'_comp (Ï†â‚ : Sâ‚ âŸ¶ Sâ‚‚) (Ï†â‚‚ : Sâ‚‚ âŸ¶ Sâ‚ƒ)
    (hâ‚ : Sâ‚.HomologyData) (hâ‚‚ : Sâ‚‚.HomologyData) (hâ‚ƒ : Sâ‚ƒ.HomologyData) :
    homologyMap' (Ï†â‚ â‰« Ï†â‚‚) hâ‚ hâ‚ƒ = homologyMap' Ï†â‚ hâ‚ hâ‚‚ â‰«
      homologyMap' Ï†â‚‚ hâ‚‚ hâ‚ƒ :=
  leftHomologyMap'_comp _ _ _ _ _

@[simp]
lemma homologyMap_comp [HasHomology Sâ‚] [HasHomology Sâ‚‚] [HasHomology Sâ‚ƒ]
    (Ï†â‚ : Sâ‚ âŸ¶ Sâ‚‚) (Ï†â‚‚ : Sâ‚‚ âŸ¶ Sâ‚ƒ) :
    homologyMap (Ï†â‚ â‰« Ï†â‚‚) = homologyMap Ï†â‚ â‰« homologyMap Ï†â‚‚ :=
homologyMap'_comp _ _ _ _ _

@[simps]
def homologyMapIso' (e : Sâ‚ â‰… Sâ‚‚) (hâ‚ : Sâ‚.HomologyData)
    (hâ‚‚ : Sâ‚‚.HomologyData) : hâ‚.left.H â‰… hâ‚‚.left.H where
  hom := homologyMap' e.hom hâ‚ hâ‚‚
  inv := homologyMap' e.inv hâ‚‚ hâ‚
  hom_inv_id := by rw [â† homologyMap'_comp, e.hom_inv_id, homologyMap'_id]
  inv_hom_id := by rw [â† homologyMap'_comp, e.inv_hom_id, homologyMap'_id]

instance isIso_homologyMap'_of_isIso (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [IsIso Ï†]
    (hâ‚ : Sâ‚.HomologyData) (hâ‚‚ : Sâ‚‚.HomologyData) :
    IsIso (homologyMap' Ï† hâ‚ hâ‚‚) :=
  (inferInstance : IsIso (homologyMapIso' (asIso Ï†) hâ‚ hâ‚‚).hom)

@[simps]
noncomputable def homologyMapIso (e : Sâ‚ â‰… Sâ‚‚) [Sâ‚.HasHomology]
  [Sâ‚‚.HasHomology] : Sâ‚.homology â‰… Sâ‚‚.homology where
  hom := homologyMap e.hom
  inv := homologyMap e.inv
  hom_inv_id := by rw [â† homologyMap_comp, e.hom_inv_id, homologyMap_id]
  inv_hom_id := by rw [â† homologyMap_comp, e.inv_hom_id, homologyMap_id]

instance isIso_homologyMap_of_iso (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [IsIso Ï†] [Sâ‚.HasHomology]
    [Sâ‚‚.HasHomology] :
    IsIso (homologyMap Ï†) :=
  (inferInstance : IsIso (homologyMapIso (asIso Ï†)).hom)

variable {S}

def leftRightHomologyComparison' (hâ‚ : S.LeftHomologyData) (hâ‚‚ : S.RightHomologyData) :
    hâ‚.H âŸ¶ hâ‚‚.H :=
  hâ‚‚.liftH (hâ‚.descH (hâ‚.i â‰« hâ‚‚.p) (by simp))
    (by rw [â† cancel_epi hâ‚.Ï€, LeftHomologyData.Ï€_descH_assoc, assoc,
      RightHomologyData.p_g', LeftHomologyData.wi, comp_zero])

lemma leftRightHomologyComparison'_eqâ‚ (hâ‚ : S.LeftHomologyData) (hâ‚‚ : S.RightHomologyData) :
    leftRightHomologyComparison' hâ‚ hâ‚‚ =
  hâ‚‚.liftH (hâ‚.descH (hâ‚.i â‰« hâ‚‚.p) (by simp))
    (by rw [â† cancel_epi hâ‚.Ï€, LeftHomologyData.Ï€_descH_assoc, assoc,
      RightHomologyData.p_g', LeftHomologyData.wi, comp_zero]) := rfl

@[reassoc (attr := simp)]
lemma Ï€_leftRightHomologyComparison'_Î¹ (hâ‚ : S.LeftHomologyData) (hâ‚‚ : S.RightHomologyData) :
    hâ‚.Ï€ â‰« leftRightHomologyComparison' hâ‚ hâ‚‚ â‰« hâ‚‚.Î¹ = hâ‚.i â‰« hâ‚‚.p :=
  by simp only [leftRightHomologyComparison'_eqâ‚,
    RightHomologyData.liftH_Î¹, LeftHomologyData.Ï€_descH]

lemma leftRightHomologyComparison'_eqâ‚‚ (hâ‚ : S.LeftHomologyData) (hâ‚‚ : S.RightHomologyData) :
    leftRightHomologyComparison' hâ‚ hâ‚‚ =
  hâ‚.descH (hâ‚‚.liftH (hâ‚.i â‰« hâ‚‚.p) (by simp))
    (by rw [â† cancel_mono hâ‚‚.Î¹, assoc, RightHomologyData.liftH_Î¹,
      LeftHomologyData.f'_i_assoc, RightHomologyData.wp, zero_comp]) := by
  simp only [â† cancel_mono hâ‚‚.Î¹, â† cancel_epi hâ‚.Ï€, Ï€_leftRightHomologyComparison'_Î¹,
    LeftHomologyData.Ï€_descH_assoc, RightHomologyData.liftH_Î¹]

variable (S)

noncomputable def leftRightHomologyComparison [S.HasLeftHomology] [S.HasRightHomology] :
    S.leftHomology âŸ¶ S.rightHomology :=
  leftRightHomologyComparison' _ _

@[reassoc (attr := simp)]
lemma Ï€_leftRightHomologyComparison_Î¹ [S.HasLeftHomology] [S.HasRightHomology] :
    S.leftHomologyÏ€ â‰« S.leftRightHomologyComparison â‰« S.rightHomologyÎ¹ =
      S.iCycles â‰« S.pOpcycles :=
  Ï€_leftRightHomologyComparison'_Î¹ _ _

@[reassoc]
lemma leftRightHomologyComparison'_naturality (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (hâ‚ : Sâ‚.LeftHomologyData)
  (hâ‚‚ : Sâ‚.RightHomologyData) (hâ‚' : Sâ‚‚.LeftHomologyData) (hâ‚‚' : Sâ‚‚.RightHomologyData) :
  leftHomologyMap' Ï† hâ‚ hâ‚' â‰« leftRightHomologyComparison' hâ‚' hâ‚‚' =
    leftRightHomologyComparison' hâ‚ hâ‚‚ â‰« rightHomologyMap' Ï† hâ‚‚ hâ‚‚' :=
by simp only [â† cancel_epi hâ‚.Ï€, â† cancel_mono hâ‚‚'.Î¹, assoc,
    leftHomologyÏ€_naturality'_assoc, rightHomologyÎ¹_naturality',
    Ï€_leftRightHomologyComparison'_Î¹,
    Ï€_leftRightHomologyComparison'_Î¹_assoc,
    cyclesMap'_i_assoc, p_opcyclesMap']

variable {S}

lemma leftRightHomologyComparison'_compatibility (hâ‚ hâ‚' : S.LeftHomologyData)
    (hâ‚‚ hâ‚‚' : S.RightHomologyData) :
    leftRightHomologyComparison' hâ‚ hâ‚‚ = leftHomologyMap' (ğŸ™ S) hâ‚ hâ‚' â‰«
      leftRightHomologyComparison' hâ‚' hâ‚‚' â‰« rightHomologyMap' (ğŸ™ S) _ _ :=
by rw [leftRightHomologyComparison'_naturality_assoc (ğŸ™ S) hâ‚ hâ‚‚ hâ‚' hâ‚‚',
    â† rightHomologyMap'_comp, comp_id, rightHomologyMap'_id, comp_id]

lemma leftRightHomologyComparison_eq [S.HasLeftHomology] [S.HasRightHomology]
    (hâ‚ : S.LeftHomologyData) (hâ‚‚ : S.RightHomologyData) :
    S.leftRightHomologyComparison = hâ‚.leftHomologyIso.hom â‰«
      leftRightHomologyComparison' hâ‚ hâ‚‚ â‰« hâ‚‚.rightHomologyIso.inv :=
leftRightHomologyComparison'_compatibility _ _ _ _

@[simp]
lemma HomologyData.leftRightHomologyComparison'_eq (h : S.HomologyData) :
    leftRightHomologyComparison' h.left h.right = h.iso.hom := by
  simp only [â†cancel_epi h.left.Ï€, â† cancel_mono h.right.Î¹,
    Ï€_leftRightHomologyComparison'_Î¹, HomologyData.comm]

instance isIso_leftRightHomologyComparison'_of_homologyData (h : S.HomologyData) :
  IsIso (leftRightHomologyComparison' h.left h.right) := by
    rw [h.leftRightHomologyComparison'_eq]
    infer_instance

instance isIso_leftRightHomologyComparison' [S.HasHomology]
    (hâ‚ : S.LeftHomologyData) (hâ‚‚ : S.RightHomologyData) :
    IsIso (leftRightHomologyComparison' hâ‚ hâ‚‚) := by
  rw [leftRightHomologyComparison'_compatibility hâ‚ S.homologyData.left hâ‚‚
    S.homologyData.right]
  infer_instance

instance isIso_leftRightHomologyComparison [S.HasHomology] :
    IsIso S.leftRightHomologyComparison := by
  dsimp only [leftRightHomologyComparison]
  infer_instance

namespace HomologyData

@[simps]
noncomputable def ofIsIsoLeftRightHomologyComparison'
    (hâ‚ : S.LeftHomologyData) (hâ‚‚ : S.RightHomologyData)
    [IsIso (leftRightHomologyComparison' hâ‚ hâ‚‚)] :
    S.HomologyData where
  left := hâ‚
  right := hâ‚‚
  iso := asIso (leftRightHomologyComparison' hâ‚ hâ‚‚)

end HomologyData

variable (S)

noncomputable def homologyData' [S.HasHomology] : S.HomologyData :=
  HomologyData.ofIsIsoLeftRightHomologyComparison'
    S.leftHomologyData S.rightHomologyData

variable {S}

lemma leftRightHomologyComparison'_eq_leftHomologpMap'_comp_iso_hom_comp_rightHomology_map'
    (h : S.HomologyData) (hâ‚ : S.LeftHomologyData) (hâ‚‚ : S.RightHomologyData) :
    leftRightHomologyComparison' hâ‚ hâ‚‚ =
      leftHomologyMap' (ğŸ™ S) hâ‚ h.left â‰« h.iso.hom â‰« rightHomologyMap' (ğŸ™ S) h.right hâ‚‚ := by
  simpa only [h.leftRightHomologyComparison'_eq] using leftRightHomologyComparison'_compatibility hâ‚ h.left hâ‚‚ h.right

@[reassoc]
lemma leftRightHomologyComparison'_fac (hâ‚ : S.LeftHomologyData) (hâ‚‚ : S.RightHomologyData)
    [S.HasHomology] :
    leftRightHomologyComparison' hâ‚ hâ‚‚ = hâ‚.homologyIso.inv â‰« hâ‚‚.homologyIso.hom := by
  rw [leftRightHomologyComparison'_eq_leftHomologpMap'_comp_iso_hom_comp_rightHomology_map'
    S.homologyData hâ‚ hâ‚‚]
  dsimp only [LeftHomologyData.homologyIso, LeftHomologyData.leftHomologyIso,
    Iso.symm, Iso.trans, Iso.refl, leftHomologyMapIso', leftHomologyIso,
    RightHomologyData.homologyIso, RightHomologyData.rightHomologyIso,
    rightHomologyMapIso', rightHomologyIso]
  simp only [assoc, â† leftHomologyMap'_comp_assoc, id_comp, â† rightHomologyMap'_comp]

variable (S)

@[reassoc]
lemma leftRightHomologyComparison_fac [S.HasHomology] :
    S.leftRightHomologyComparison = S.leftHomologyIso.hom â‰« S.rightHomologyIso.inv := by
  simpa only [LeftHomologyData.homologyIso_leftHomologyData, Iso.symm_inv,
    RightHomologyData.homologyIso_rightHomologyData, Iso.symm_hom] using
      leftRightHomologyComparison'_fac S.leftHomologyData S.rightHomologyData

variable {S}

lemma hasHomology_of_isIso_leftRightHomologyComparison'
    (hâ‚ : S.LeftHomologyData) (hâ‚‚ : S.RightHomologyData)
    [IsIso (leftRightHomologyComparison' hâ‚ hâ‚‚)] :
    S.HasHomology :=
  HasHomology.mk' (HomologyData.ofIsIsoLeftRightHomologyComparison' hâ‚ hâ‚‚)

lemma hasHomology_of_isIsoLeftRightHomologyComparison [S.HasLeftHomology]
    [S.HasRightHomology] [h : IsIso S.leftRightHomologyComparison] :
    S.HasHomology := by
  haveI : IsIso (leftRightHomologyComparison' S.leftHomologyData S.rightHomologyData) := h
  exact hasHomology_of_isIso_leftRightHomologyComparison' S.leftHomologyData S.rightHomologyData

@[reassoc]
lemma LeftHomologyData.leftHomologyIso_hom_naturality [Sâ‚.HasHomology] [Sâ‚‚.HasHomology]
    (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (hâ‚ : Sâ‚.LeftHomologyData) (hâ‚‚ : Sâ‚‚.LeftHomologyData) :
    hâ‚.homologyIso.hom â‰« leftHomologyMap' Ï† hâ‚ hâ‚‚ =
      homologyMap Ï† â‰« hâ‚‚.homologyIso.hom := by
  dsimp [homologyIso, ShortComplex.leftHomologyIso, homologyMap, homologyMap', leftHomologyIso]
  simp only [â† leftHomologyMap'_comp, id_comp, comp_id]

@[reassoc]
lemma LeftHomologyData.leftHomologyIso_inv_naturality [Sâ‚.HasHomology] [Sâ‚‚.HasHomology]
    (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (hâ‚ : Sâ‚.LeftHomologyData) (hâ‚‚ : Sâ‚‚.LeftHomologyData) :
      hâ‚.homologyIso.inv â‰« homologyMap Ï† =
       leftHomologyMap' Ï† hâ‚ hâ‚‚ â‰« hâ‚‚.homologyIso.inv := by
  dsimp [homologyIso, ShortComplex.leftHomologyIso, homologyMap, homologyMap', leftHomologyIso]
  simp only [â† leftHomologyMap'_comp, id_comp, comp_id]

@[reassoc]
lemma leftHomologyIso_hom_naturality [Sâ‚.HasHomology] [Sâ‚‚.HasHomology] (Ï† : Sâ‚ âŸ¶ Sâ‚‚) :
    Sâ‚.leftHomologyIso.hom â‰« homologyMap Ï† =
      leftHomologyMap Ï† â‰« Sâ‚‚.leftHomologyIso.hom := by
  simpa only [LeftHomologyData.homologyIso_leftHomologyData, Iso.symm_inv] using
    LeftHomologyData.leftHomologyIso_inv_naturality Ï† Sâ‚.leftHomologyData Sâ‚‚.leftHomologyData

@[reassoc]
lemma leftHomologyIso_inv_naturality [Sâ‚.HasHomology] [Sâ‚‚.HasHomology] (Ï† : Sâ‚ âŸ¶ Sâ‚‚) :
    Sâ‚.leftHomologyIso.inv â‰« leftHomologyMap Ï† =
      homologyMap Ï† â‰« Sâ‚‚.leftHomologyIso.inv := by
  simpa only [LeftHomologyData.homologyIso_leftHomologyData, Iso.symm_inv] using
    LeftHomologyData.leftHomologyIso_hom_naturality Ï† Sâ‚.leftHomologyData Sâ‚‚.leftHomologyData

@[reassoc]
lemma RightHomologyData.rightHomologyIso_hom_naturality [Sâ‚.HasHomology] [Sâ‚‚.HasHomology]
    (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (hâ‚ : Sâ‚.RightHomologyData) (hâ‚‚ : Sâ‚‚.RightHomologyData) :
    hâ‚.homologyIso.hom â‰« rightHomologyMap' Ï† hâ‚ hâ‚‚ =
      homologyMap Ï† â‰« hâ‚‚.homologyIso.hom := by
  rw [â† cancel_epi hâ‚.homologyIso.inv, Iso.inv_hom_id_assoc,
    â† cancel_epi (leftRightHomologyComparison' Sâ‚.leftHomologyData hâ‚),
    â† leftRightHomologyComparison'_naturality Ï† Sâ‚.leftHomologyData hâ‚ Sâ‚‚.leftHomologyData hâ‚‚,
    â† cancel_epi (Sâ‚.leftHomologyData.homologyIso.hom),
    LeftHomologyData.leftHomologyIso_hom_naturality_assoc,
    leftRightHomologyComparison'_fac, leftRightHomologyComparison'_fac, assoc,
    Iso.hom_inv_id_assoc, Iso.hom_inv_id_assoc, Iso.hom_inv_id_assoc]

@[reassoc]
lemma RightHomologyData.rightHomologyIso_inv_naturality [Sâ‚.HasHomology] [Sâ‚‚.HasHomology]
    (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (hâ‚ : Sâ‚.RightHomologyData) (hâ‚‚ : Sâ‚‚.RightHomologyData) :
      hâ‚.homologyIso.inv â‰« homologyMap Ï† =
        rightHomologyMap' Ï† hâ‚ hâ‚‚ â‰« hâ‚‚.homologyIso.inv := by
  simp only [â† cancel_mono hâ‚‚.homologyIso.hom, assoc,
    â† RightHomologyData.rightHomologyIso_hom_naturality Ï† hâ‚ hâ‚‚, Iso.inv_hom_id,
    Iso.inv_hom_id_assoc, comp_id]

@[reassoc]
lemma rightHomologyIso_hom_naturality [Sâ‚.HasHomology] [Sâ‚‚.HasHomology] (Ï† : Sâ‚ âŸ¶ Sâ‚‚) :
    Sâ‚.rightHomologyIso.hom â‰« homologyMap Ï† =
      rightHomologyMap Ï† â‰« Sâ‚‚.rightHomologyIso.hom := by
  simpa only [RightHomologyData.homologyIso_rightHomologyData, Iso.symm_inv] using
    RightHomologyData.rightHomologyIso_inv_naturality Ï† Sâ‚.rightHomologyData Sâ‚‚.rightHomologyData

@[reassoc]
lemma rightHomologyIso_inv_naturality [Sâ‚.HasHomology] [Sâ‚‚.HasHomology] (Ï† : Sâ‚ âŸ¶ Sâ‚‚) :
    Sâ‚.rightHomologyIso.inv â‰« rightHomologyMap Ï† =
      homologyMap Ï† â‰« Sâ‚‚.rightHomologyIso.inv := by
  simpa only [RightHomologyData.homologyIso_rightHomologyData, Iso.symm_inv] using
    RightHomologyData.rightHomologyIso_hom_naturality Ï† Sâ‚.rightHomologyData Sâ‚‚.rightHomologyData

variable (C)

class _root_.CategoryTheory.CategoryWithHomology : Prop where
  hasHomology : âˆ€ (S : ShortComplex C), S.HasHomology

attribute [instance] CategoryWithHomology.hasHomology

instance [CategoryWithHomology C] : CategoryWithHomology Cáµ’áµ– :=
  âŸ¨fun S => HasHomology.mk' S.unop.homologyData.opâŸ©

@[simps]
noncomputable def homologyFunctor [CategoryWithHomology C] :
    ShortComplex C â¥¤ C where
  obj S := S.homology
  map f := homologyMap f

variable {C}

instance isIso_homologyMap'_of_epi_of_isIso_of_mono (Ï† : Sâ‚ âŸ¶ Sâ‚‚)
    (hâ‚ : Sâ‚.HomologyData) (hâ‚‚ : Sâ‚‚.HomologyData) [Epi Ï†.Ï„â‚] [IsIso Ï†.Ï„â‚‚] [Mono Ï†.Ï„â‚ƒ] :
    IsIso (homologyMap' Ï† hâ‚ hâ‚‚) := by
  dsimp only [homologyMap']
  infer_instance

lemma isIso_homologyMap_of_epi_of_isIso_of_mono' (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [Sâ‚.HasHomology] [Sâ‚‚.HasHomology]
    (hâ‚ : Epi Ï†.Ï„â‚) (hâ‚‚ : IsIso Ï†.Ï„â‚‚) (hâ‚ƒ : Mono Ï†.Ï„â‚ƒ) :
    IsIso (homologyMap Ï†) := by
  dsimp only [homologyMap]
  infer_instance

instance isIso_homologyMap_of_epi_of_isIso_of_mono (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [Sâ‚.HasHomology] [Sâ‚‚.HasHomology]
    [Epi Ï†.Ï„â‚] [IsIso Ï†.Ï„â‚‚] [Mono Ï†.Ï„â‚ƒ] :
    IsIso (homologyMap Ï†) :=
  isIso_homologyMap_of_epi_of_isIso_of_mono' Ï† inferInstance inferInstance inferInstance

instance isIso_homologyFunctor_map_of_epi_of_isIso_of_mono (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [CategoryWithHomology C]
    [Epi Ï†.Ï„â‚] [IsIso Ï†.Ï„â‚‚] [Mono Ï†.Ï„â‚ƒ] :
    IsIso ((homologyFunctor C).map Ï†) :=
  (inferInstance : IsIso (homologyMap Ï†))

instance isIso_homologyMap_of_isIso (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [Sâ‚.HasHomology] [Sâ‚‚.HasHomology] [IsIso Ï†] :
    IsIso (homologyMap Ï†) := by
  dsimp only [homologyMap, homologyMap']
  infer_instance

section

variable (S) {A : C}
variable [HasHomology S]

noncomputable def homologyÏ€ : S.cycles âŸ¶ S.homology :=
  S.leftHomologyÏ€ â‰« S.leftHomologyIso.hom

noncomputable def homologyÎ¹ : S.homology âŸ¶ S.opcycles :=
  S.rightHomologyIso.inv â‰« S.rightHomologyÎ¹

@[reassoc (attr := simp)]
lemma homologyÏ€_comp_leftHomologyIso_inv:
    S.homologyÏ€ â‰« S.leftHomologyIso.inv = S.leftHomologyÏ€ := by
  dsimp only [homologyÏ€]
  simp only [assoc, Iso.hom_inv_id, comp_id]

@[reassoc (attr := simp)]
lemma rightHomologyIso_hom_comp_homologyÎ¹ :
    S.rightHomologyIso.hom â‰« S.homologyÎ¹ = S.rightHomologyÎ¹ := by
  dsimp only [homologyÎ¹]
  simp only [Iso.hom_inv_id_assoc]

@[reassoc (attr := simp)]
lemma toCycles_comp_homologyÏ€ :
    S.toCycles â‰« S.homologyÏ€ = 0 := by
  dsimp only [homologyÏ€]
  simp only [toCycles_comp_leftHomologyÏ€_assoc, zero_comp]

@[reassoc (attr := simp)]
lemma homologyÎ¹_comp_fromOpcycles :
    S.homologyÎ¹ â‰« S.fromOpcycles = 0 := by
  dsimp only [homologyÎ¹]
  simp only [assoc, rightHomologyÎ¹_comp_fromOpcycles, comp_zero]

noncomputable def homologyIsCokernel :
  IsColimit (CokernelCofork.ofÏ€ S.homologyÏ€ S.toCycles_comp_homologyÏ€) :=
IsColimit.ofIsoColimit S.leftHomologyIsCokernel
  (Cofork.ext S.leftHomologyIso rfl)

noncomputable def homologyIsKernel :
  IsLimit (KernelFork.ofÎ¹ S.homologyÎ¹ S.homologyÎ¹_comp_fromOpcycles) :=
IsLimit.ofIsoLimit S.rightHomologyIsKernel
  (Fork.ext S.rightHomologyIso (by simp))

instance : Epi S.homologyÏ€ :=
  Limits.epi_of_isColimit_cofork (S.homologyIsCokernel)

instance : Mono S.homologyÎ¹ :=
  Limits.mono_of_isLimit_fork (S.homologyIsKernel)

noncomputable def descHomology (k : S.cycles âŸ¶ A) (hk : S.toCycles â‰« k = 0) :
    S.homology âŸ¶ A :=
  S.homologyIsCokernel.desc (CokernelCofork.ofÏ€ k hk)

noncomputable def liftHomology (k : A âŸ¶ S.opcycles) (hk : k â‰« S.fromOpcycles = 0) :
    A âŸ¶ S.homology :=
  S.homologyIsKernel.lift (KernelFork.ofÎ¹ k hk)

@[reassoc (attr := simp)]
lemma Ï€_descHomology (k : S.cycles âŸ¶ A) (hk : S.toCycles â‰« k = 0) :
  S.homologyÏ€ â‰« S.descHomology k hk = k :=
Cofork.IsColimit.Ï€_desc S.homologyIsCokernel

@[reassoc (attr := simp)]
lemma liftHomology_Î¹ (k : A âŸ¶ S.opcycles) (hk : k â‰« S.fromOpcycles = 0) :
  S.liftHomology k hk â‰« S.homologyÎ¹ = k :=
  Fork.IsLimit.lift_Î¹ S.homologyIsKernel

@[reassoc (attr := simp)]
lemma homologyÏ€_naturality (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [Sâ‚.HasHomology] [Sâ‚‚.HasHomology] :
    Sâ‚.homologyÏ€ â‰« homologyMap Ï† = cyclesMap Ï† â‰« Sâ‚‚.homologyÏ€ := by
  simp only [â† cancel_mono Sâ‚‚.leftHomologyIso.inv, assoc, â† leftHomologyIso_inv_naturality Ï†,
    homologyÏ€_comp_leftHomologyIso_inv, â† leftHomologyÏ€_naturality]
  simp only [homologyÏ€, assoc, Iso.hom_inv_id_assoc, leftHomologyÏ€_naturality]

@[reassoc (attr := simp)]
lemma homologyÎ¹_naturality (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [Sâ‚.HasHomology] [Sâ‚‚.HasHomology] :
    homologyMap Ï† â‰« Sâ‚‚.homologyÎ¹ = Sâ‚.homologyÎ¹ â‰« Sâ‚.opcyclesMap Ï†  := by
  simp only [â† cancel_epi Sâ‚.rightHomologyIso.hom, rightHomologyIso_hom_naturality_assoc Ï†,
    rightHomologyIso_hom_comp_homologyÎ¹, rightHomologyÎ¹_naturality]
  simp only [homologyÎ¹, assoc, Iso.hom_inv_id_assoc]

@[reassoc (attr := simp)]
lemma homology_Ï€_Î¹ :
    S.homologyÏ€ â‰« S.homologyÎ¹ = S.iCycles â‰« S.pOpcycles := by
  dsimp only [homologyÏ€, homologyÎ¹]
  simpa only [assoc, S.leftRightHomologyComparison_fac] using S.Ï€_leftRightHomologyComparison_Î¹

noncomputable def homologyIsoKernelDesc [S.HasHomology] [HasCokernel S.f]
  [HasKernel (cokernel.desc S.f S.g S.zero)] :
  S.homology â‰… kernel (cokernel.desc S.f S.g S.zero) :=
  S.rightHomologyIso.symm â‰ªâ‰« S.rightHomologyIsoKernelDesc

noncomputable def homologyIsoCokernelLift [S.HasHomology] [HasKernel S.g]
  [HasCokernel (kernel.lift S.g S.f S.zero)] :
  S.homology â‰… cokernel (kernel.lift S.g S.f S.zero) :=
  S.leftHomologyIso.symm â‰ªâ‰« S.leftHomologyIsoCokernelLift

@[reassoc (attr := simp)]
lemma LeftHomologyData.homologyÏ€_comp_homologyIso_hom
    (h : S.LeftHomologyData) [S.HasHomology] :
    S.homologyÏ€ â‰« h.homologyIso.hom = h.cyclesIso.hom â‰« h.Ï€ := by
  dsimp only [homologyÏ€, homologyIso]
  simp only [Iso.trans_hom, Iso.symm_hom, assoc, Iso.hom_inv_id_assoc,
    leftHomologyÏ€_comp_leftHomologyIso_hom]

@[reassoc (attr := simp)]
lemma LeftHomologyData.Ï€_comp_homologyIso_inv (h : S.LeftHomologyData) [S.HasHomology] :
    h.Ï€ â‰« h.homologyIso.inv = h.cyclesIso.inv â‰« S.homologyÏ€ := by
  dsimp only [homologyÏ€, homologyIso]
  simp only [Iso.trans_inv, Iso.symm_inv, Ï€_comp_leftHomologyIso_inv_assoc]

@[reassoc (attr := simp)]
lemma RightHomologyData.homologyIso_inv_comp_homologyÎ¹
    (h : S.RightHomologyData) [S.HasHomology] :
    h.homologyIso.inv â‰« S.homologyÎ¹ = h.Î¹ â‰« h.opcyclesIso.inv := by
  dsimp only [homologyÎ¹, homologyIso]
  simp only [Iso.trans_inv, Iso.symm_inv, assoc, Iso.hom_inv_id_assoc,
    rightHomologyIso_inv_comp_rightHomologyÎ¹]

@[reassoc (attr := simp)]
lemma RightHomologyData.homologyIso_hom_comp_Î¹
    (h : S.RightHomologyData) [S.HasHomology] :
    h.homologyIso.hom â‰« h.Î¹ = S.homologyÎ¹ â‰« h.opcyclesIso.hom := by
  dsimp only [homologyÎ¹, homologyIso]
  simp only [Iso.trans_hom, Iso.symm_hom, assoc, rightHomologyIso_hom_comp_Î¹]

@[reassoc (attr := simp)]
lemma LeftHomologyData.homologyIso_hom_comp_leftHomologyIso_inv
    (h : S.LeftHomologyData) [S.HasHomology] :
    h.homologyIso.hom â‰« h.leftHomologyIso.inv =
      S.leftHomologyIso.inv := by
  dsimp only [homologyIso]
  simp only [Iso.trans_hom, Iso.symm_hom, assoc, Iso.hom_inv_id, comp_id]

@[reassoc (attr := simp)]
lemma LeftHomologyData.leftHomologyIso_hom_comp_homologyIso_inv
    (h : S.LeftHomologyData) [S.HasHomology] :
    h.leftHomologyIso.hom â‰« h.homologyIso.inv =
      S.leftHomologyIso.hom := by
  dsimp only [homologyIso]
  simp only [Iso.trans_inv, Iso.symm_inv, Iso.hom_inv_id_assoc]

@[reassoc (attr := simp)]
lemma RightHomologyData.homologyIso_hom_comp_rightHomologyIso_inv
    (h : S.RightHomologyData) [S.HasHomology] :
    h.homologyIso.hom â‰« h.rightHomologyIso.inv =
      S.rightHomologyIso.inv := by
  dsimp only [homologyIso]
  simp only [Iso.trans_hom, Iso.symm_hom, assoc, Iso.hom_inv_id, comp_id]

@[reassoc (attr := simp)]
lemma RightHomologyData.rightHomologyIso_hom_comp_homologyIso_inv
    (h : S.RightHomologyData) [S.HasHomology] :
    h.rightHomologyIso.hom â‰« h.homologyIso.inv =
      S.rightHomologyIso.hom := by
  dsimp only [homologyIso]
  simp only [Iso.trans_inv, Iso.symm_inv, Iso.hom_inv_id_assoc]

@[reassoc]
lemma comp_homologyMap_comp [Sâ‚.HasHomology] [Sâ‚‚.HasHomology] (Ï† : Sâ‚ âŸ¶ Sâ‚‚)
    (hâ‚ : Sâ‚.LeftHomologyData) (hâ‚‚ : Sâ‚‚.RightHomologyData) :
    hâ‚.Ï€ â‰« hâ‚.homologyIso.inv â‰« homologyMap Ï† â‰« hâ‚‚.homologyIso.hom â‰« hâ‚‚.Î¹ =
      hâ‚.i â‰« Ï†.Ï„â‚‚ â‰« hâ‚‚.p := by
  dsimp only [LeftHomologyData.homologyIso, RightHomologyData.homologyIso,
    Iso.symm, Iso.trans, Iso.refl, leftHomologyIso, rightHomologyIso,
    leftHomologyMapIso', rightHomologyMapIso',
    LeftHomologyData.cyclesIso, RightHomologyData.opcyclesIso,
    LeftHomologyData.leftHomologyIso, RightHomologyData.rightHomologyIso,
    homologyMap, homologyMap']
  simp only [assoc, rightHomologyÎ¹_naturality', rightHomologyÎ¹_naturality'_assoc,
    leftHomologyÏ€_naturality'_assoc, HomologyData.comm_assoc, p_opcyclesMap'_assoc,
    id_Ï„â‚‚, p_opcyclesMap', id_comp, cyclesMap'_i_assoc]

@[reassoc]
lemma Ï€_homologyMap_Î¹ [Sâ‚.HasHomology] [Sâ‚‚.HasHomology] (Ï† : Sâ‚ âŸ¶ Sâ‚‚) :
    Sâ‚.homologyÏ€ â‰« homologyMap Ï† â‰« Sâ‚‚.homologyÎ¹ =
      Sâ‚.iCycles â‰« Ï†.Ï„â‚‚ â‰« Sâ‚‚.pOpcycles := by
  simp only [homologyÎ¹_naturality, homology_Ï€_Î¹_assoc, p_opcyclesMap]

end

variable (S)

noncomputable def homologyOpIso [S.HasHomology] :
    S.op.homology â‰… Opposite.op S.homology :=
  S.op.leftHomologyIso.symm â‰ªâ‰« S.leftHomologyOpIso â‰ªâ‰« S.rightHomologyIso.symm.op

@[simp]
lemma homologyMap'_op : (homologyMap' Ï† hâ‚ hâ‚‚).op =
    hâ‚‚.iso.inv.op â‰« homologyMap' (opMap Ï†) hâ‚‚.op hâ‚.op â‰« hâ‚.iso.hom.op := Quiver.Hom.unop_inj (by
  dsimp
  have Î³ : HomologyMapData Ï† hâ‚ hâ‚‚ := default
  simp only [Î³.homologyMap'_eq, Î³.op.homologyMap'_eq, HomologyData.op_left,
    HomologyMapData.op_left, RightHomologyMapData.op_Ï†H, Quiver.Hom.unop_op, assoc,
    â† Î³.comm_assoc, Iso.hom_inv_id, comp_id])

@[simp]
lemma homologyMap_op [HasHomology Sâ‚] [HasHomology Sâ‚‚] : (homologyMap Ï†).op =
    (Sâ‚‚.homologyOpIso).inv â‰« homologyMap (opMap Ï†) â‰« (Sâ‚.homologyOpIso).hom := by
  dsimp only [homologyMap, homologyOpIso]
  rw [homologyMap'_op]
  dsimp only [Iso.symm, Iso.trans, Iso.op, Iso.refl, rightHomologyIso, leftHomologyIso,
    leftHomologyOpIso, leftHomologyMapIso', rightHomologyMapIso',
    LeftHomologyData.leftHomologyIso, homologyMap']
  simp only [assoc, rightHomologyMap'_op, op_comp, â† leftHomologyMap'_comp_assoc, id_comp,
    opMap_id, comp_id, HomologyData.op_left]

variable (C)

noncomputable def homologyFunctorOpNatIso [CategoryWithHomology C] :
    (homologyFunctor C).op â‰… opFunctor C â‹™ homologyFunctor Cáµ’áµ– :=
  NatIso.ofComponents (fun S => Iso.symm S.unop.homologyOpIso) (by simp)

variable {C}

lemma liftCycles_homologyÏ€_eq_zero_of_boundary [S.HasHomology]
    (k : A âŸ¶ S.Xâ‚‚) (x : A âŸ¶ S.Xâ‚) (hx : k = x â‰« S.f) :
    S.liftCycles k (by rw [hx, assoc, S.zero, comp_zero]) â‰« S.homologyÏ€ = 0 := by
  dsimp only [homologyÏ€]
  rw [S.liftCycles_leftHomologyÏ€_eq_zero_of_boundary_assoc k x hx, zero_comp]

@[reassoc]
lemma homologyÎ¹_descOpcycles_Ï€_eq_zero_of_boundary [S.HasHomology]
    (x : S.Xâ‚ƒ âŸ¶ A) (hx : k = S.g â‰« x) :
    S.homologyÎ¹ â‰« S.descOpcycles k (by rw [hx, S.zero_assoc, zero_comp]) = 0 := by
  dsimp only [homologyÎ¹]
  rw [assoc, S.rightHomologyÎ¹_descOpcycles_Ï€_eq_zero_of_boundary k x hx, comp_zero]

lemma isIso_homologyMap_of_isIso_cyclesMap_of_epi (Ï† : Sâ‚ âŸ¶ Sâ‚‚)
    [Sâ‚.HasHomology] [Sâ‚‚.HasHomology] (hâ‚ : IsIso (cyclesMap Ï†)) (hâ‚‚ : Epi Ï†.Ï„â‚) :
    IsIso (homologyMap Ï†) := by
  have h : Sâ‚‚.toCycles â‰« inv (cyclesMap Ï†) â‰« Sâ‚.homologyÏ€ = 0 := by
    simp only [â† cancel_epi Ï†.Ï„â‚, â† toCycles_naturality_assoc,
      IsIso.hom_inv_id_assoc, toCycles_comp_homologyÏ€, comp_zero]
  have âŸ¨z, hzâŸ© := CokernelCofork.IsColimit.desc' Sâ‚‚.homologyIsCokernel _ h
  dsimp at hz
  refine' âŸ¨âŸ¨z, _, _âŸ©âŸ©
  . rw [â† cancel_epi Sâ‚.homologyÏ€, homologyÏ€_naturality_assoc, hz,
      IsIso.hom_inv_id_assoc, comp_id]
  . rw [â† cancel_epi Sâ‚‚.homologyÏ€, reassoc_of% hz,
      homologyÏ€_naturality, IsIso.inv_hom_id_assoc, comp_id]

lemma isIso_homologyMap_of_isIso_opcyclesMap_of_mono (Ï† : Sâ‚ âŸ¶ Sâ‚‚)
    [Sâ‚.HasHomology] [Sâ‚‚.HasHomology] (hâ‚ : IsIso (opcyclesMap Ï†)) (hâ‚‚ : Mono Ï†.Ï„â‚ƒ) :
    IsIso (homologyMap Ï†) := by
  have h : (Sâ‚‚.homologyÎ¹ â‰« inv (opcyclesMap Ï†)) â‰« Sâ‚.fromOpcycles = 0 := by
    simp only [â† cancel_mono Ï†.Ï„â‚ƒ, zero_comp, assoc, â† fromOpcycles_naturality,
      IsIso.inv_hom_id_assoc, homologyÎ¹_comp_fromOpcycles]
  have âŸ¨z, hzâŸ© := KernelFork.IsLimit.lift' Sâ‚.homologyIsKernel _ h
  dsimp at hz
  refine' âŸ¨âŸ¨z, _, _âŸ©âŸ©
  . rw [â† cancel_mono Sâ‚.homologyÎ¹, id_comp, assoc, hz, homologyÎ¹_naturality_assoc,
      IsIso.hom_inv_id, comp_id]
  . rw [â† cancel_mono Sâ‚‚.homologyÎ¹, assoc, homologyÎ¹_naturality, reassoc_of% hz,
      IsIso.inv_hom_id, comp_id, id_comp]

lemma isZero_homology_of_isZero_Xâ‚‚ (hS : IsZero S.Xâ‚‚) [S.HasHomology] :
    IsZero S.homology :=
  IsZero.of_iso hS (HomologyData.ofZeros S (hS.eq_of_tgt _ _)
    (hS.eq_of_src _ _)).left.homologyIso


lemma isIso_homologyÏ€ (hf : S.f = 0) [S.HasHomology] :
    IsIso S.homologyÏ€ := by
  have := S.isIso_leftHomologyÏ€ hf
  dsimp only [homologyÏ€]
  infer_instance

lemma isIso_homologyÎ¹ (hg : S.g = 0) [S.HasHomology] :
    IsIso S.homologyÎ¹ := by
  have := S.isIso_rightHomologyÎ¹ hg
  dsimp only [homologyÎ¹]
  infer_instance

@[simps! hom]
noncomputable def asIsoHomologyÏ€ (hf : S.f = 0) [S.HasHomology] :
    S.cycles â‰… S.homology := by
  have := S.isIso_homologyÏ€ hf
  exact asIso S.homologyÏ€

@[reassoc (attr := simp)]
lemma asIsoHomologyÏ€_inv_comp_homologyÏ€ (hf : S.f = 0) [S.HasHomology] :
  (S.asIsoHomologyÏ€ hf).inv â‰« S.homologyÏ€ = ğŸ™ _ := Iso.inv_hom_id _

@[reassoc (attr := simp)]
lemma homologyÏ€_comp_asIsoHomologyÏ€_inv (hf : S.f = 0) [S.HasHomology] :
  S.homologyÏ€ â‰« (S.asIsoHomologyÏ€ hf).inv  = ğŸ™ _ := (S.asIsoHomologyÏ€ hf).hom_inv_id

@[simps! hom]
noncomputable def asIsoHomologyÎ¹ (hg : S.g = 0) [S.HasHomology] :
    S.homology â‰… S.opcycles := by
  have := S.isIso_homologyÎ¹ hg
  exact asIso S.homologyÎ¹

@[reassoc (attr := simp)]
lemma asIsoHomologyÎ¹_inv_comp_homologyÎ¹ (hg : S.g = 0) [S.HasHomology] :
  (S.asIsoHomologyÎ¹ hg).inv â‰« S.homologyÎ¹ = ğŸ™ _ := Iso.inv_hom_id _

@[reassoc (attr := simp)]
lemma homologyÎ¹_comp_asIsoHomologyÎ¹_inv (hg : S.g = 0) [S.HasHomology] :
  S.homologyÎ¹ â‰« (S.asIsoHomologyÎ¹ hg).inv  = ğŸ™ _ := (S.asIsoHomologyÎ¹ hg).hom_inv_id

lemma mono_homologyMap_of_mono_opcyclesMap' [Sâ‚.HasHomology] [Sâ‚‚.HasHomology]
    (h : Mono (opcyclesMap Ï†)) :
    Mono (homologyMap Ï†) := by
  have : Mono (homologyMap Ï† â‰« Sâ‚‚.homologyÎ¹) := by
    rw [homologyÎ¹_naturality Ï†]
    apply mono_comp
  exact mono_of_mono (homologyMap Ï†) Sâ‚‚.homologyÎ¹

instance mono_homologyMap_of_mono_opcyclesMap [Sâ‚.HasHomology] [Sâ‚‚.HasHomology]
    [Mono (opcyclesMap Ï†)] :
    Mono (homologyMap Ï†) :=
  mono_homologyMap_of_mono_opcyclesMap' Ï† inferInstance

lemma epi_homologyMap_of_epi_cyclesMap' [Sâ‚.HasHomology] [Sâ‚‚.HasHomology]
    (h : Epi (cyclesMap Ï†)) :
    Epi (homologyMap Ï†) := by
  have : Epi (Sâ‚.homologyÏ€ â‰« homologyMap Ï†) := by
    rw [homologyÏ€_naturality Ï†]
    apply epi_comp
  exact epi_of_epi Sâ‚.homologyÏ€ (homologyMap Ï†)

instance epi_homologyMap_of_epi_cyclesMap [Sâ‚.HasHomology] [Sâ‚‚.HasHomology]
    [Epi (cyclesMap Ï†)] :
    Epi (homologyMap Ï†) :=
  epi_homologyMap_of_epi_cyclesMap' Ï† inferInstance

end ShortComplex

end CategoryTheory
