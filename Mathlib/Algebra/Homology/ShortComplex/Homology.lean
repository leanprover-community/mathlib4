/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/

import Mathlib.Algebra.Homology.ShortComplex.RightHomology

/-!
# Homology of short complexes

In this file, we shall define the homology of short complexes `S`, i.e. diagrams
`f : X‚ÇÅ ‚ü∂ X‚ÇÇ` and `g : X‚ÇÇ ‚ü∂ X‚ÇÉ` such that `f ‚â´ g = 0`. We shall say that
`[S.HasHomology]` when there exists `h : S.HomologyData`. A homology data
for `S` consists of compatible left/right homology data `left` and `right`. The
left homology data `left` involves an object `left.H` that is a cokernel of the canonical
map `S.X‚ÇÅ ‚ü∂ K` where `K` is a kernel of `g`. On the other hand, the dual notion `right.H`
is a kernel of the canonical morphism `Q ‚ü∂ S.X‚ÇÉ` when `Q` is a cokernel of `f`.
The compatibility that is required involves an isomorphism `left.H ‚âÖ right.H` which
makes a certain pentagon commute. When such a homology data exists, `S.homology`
shall be defined as `h.left.H` for a chosen `h : S.HomologyData`.

This definition requires very little assumption on the category (only the existence
of zero morphisms). We shall prove that in abelian categories, all short complexes
have homology data.

Note: This definition arose by the end of the Liquid Tensor Experiment which
contained a structure `has_homology` which is quite similar to `S.HomologyData`.
After the category `ShortComplex C` was introduced by J. Riou, A. Topaz suggested
such a structure could be used as a basis for the *definition* of homology.

-/

universe v u

namespace CategoryTheory

open Category Limits

variable {C : Type u} [Category.{v} C] [HasZeroMorphisms C] (S : ShortComplex C)
  {S‚ÇÅ S‚ÇÇ S‚ÇÉ S‚ÇÑ : ShortComplex C}

namespace ShortComplex

/-- A homology data for a short complex consists of two compatible left and
right homology data -/
structure HomologyData where
  /-- a left homology data -/
  left : S.LeftHomologyData
  /-- a right homology data -/
  right : S.RightHomologyData
  /-- the compatibility isomorphism relating the two dual notions of
    `LeftHomologyData` and `RightHomologyData`  -/
  iso : left.H ‚âÖ right.H
  /-- the pentagon relation expressing the compatibility of the left
  and right homology data -/
  comm : left.œÄ ‚â´ iso.hom ‚â´ right.Œπ = left.i ‚â´ right.p := by aesop_cat

attribute [reassoc (attr := simp)] HomologyData.comm

variable (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) (h‚ÇÅ : S‚ÇÅ.HomologyData) (h‚ÇÇ : S‚ÇÇ.HomologyData)

/-- A homology map data for a morphism `œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ` where both `S‚ÇÅ` and `S‚ÇÇ` are
equipped with homology data consists of left and right homology map data. -/
structure HomologyMapData where
  /-- a left homology map data -/
  left : LeftHomologyMapData œÜ h‚ÇÅ.left h‚ÇÇ.left
  /-- a right homology map data -/
  right : RightHomologyMapData œÜ h‚ÇÅ.right h‚ÇÇ.right

namespace HomologyMapData

attribute [nolint simpNF] mk.injEq

variable {œÜ h‚ÇÅ h‚ÇÇ}

@[reassoc]
lemma comm (h : HomologyMapData œÜ h‚ÇÅ h‚ÇÇ) :
    h.left.œÜH ‚â´ h‚ÇÇ.iso.hom = h‚ÇÅ.iso.hom ‚â´ h.right.œÜH := by
  simp only [‚Üê cancel_epi h‚ÇÅ.left.œÄ, ‚Üê cancel_mono h‚ÇÇ.right.Œπ, assoc,
    LeftHomologyMapData.commœÄ_assoc, HomologyData.comm, LeftHomologyMapData.commi_assoc,
    RightHomologyMapData.commŒπ, HomologyData.comm_assoc, RightHomologyMapData.commp]

instance : Subsingleton (HomologyMapData œÜ h‚ÇÅ h‚ÇÇ) := ‚ü®by
  rintro ‚ü®left‚ÇÅ, right‚ÇÅ‚ü© ‚ü®left‚ÇÇ, right‚ÇÇ‚ü©
  simp only [mk.injEq, eq_iff_true_of_subsingleton, and_self]‚ü©

instance : Inhabited (HomologyMapData œÜ h‚ÇÅ h‚ÇÇ) :=
  ‚ü®‚ü®default, default‚ü©‚ü©

instance : Unique (HomologyMapData œÜ h‚ÇÅ h‚ÇÇ) := Unique.mk' _

variable (œÜ h‚ÇÅ h‚ÇÇ)

/-- A choice of the (unique) homology map data associated with a morphism
`œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ` where both short complexes `S‚ÇÅ` and `S‚ÇÇ` are equipped with
homology data. -/
def homologyMapData : HomologyMapData œÜ h‚ÇÅ h‚ÇÇ := default

variable {œÜ h‚ÇÅ h‚ÇÇ}

lemma congr_left_œÜH {Œ≥‚ÇÅ Œ≥‚ÇÇ : HomologyMapData œÜ h‚ÇÅ h‚ÇÇ} (eq : Œ≥‚ÇÅ = Œ≥‚ÇÇ) :
  Œ≥‚ÇÅ.left.œÜH = Œ≥‚ÇÇ.left.œÜH := by rw [eq]

end HomologyMapData

namespace HomologyData

/-- When the first map `S.f` is zero, this is the homology data on `S` given
by any limit kernel fork of `S.g` -/
@[simps]
def ofIsLimitKernelFork (hf : S.f = 0) (c : KernelFork S.g) (hc : IsLimit c) :
    S.HomologyData where
  left := LeftHomologyData.ofIsLimitKernelFork S hf c hc
  right := RightHomologyData.ofIsLimitKernelFork S hf c hc
  iso := Iso.refl _

/-- When the first map `S.f` is zero, this is the homology data on `S` given
by the chosen `kernel S.g` -/
@[simps]
noncomputable def ofHasKernel (hf : S.f = 0) [HasKernel S.g] :
    S.HomologyData where
  left := LeftHomologyData.ofHasKernel S hf
  right := RightHomologyData.ofHasKernel S hf
  iso := Iso.refl _

/-- When the second map `S.g` is zero, this is the homology data on `S` given
by any colimit cokernel cofork of `S.f` -/
@[simps]
def ofIsColimitCokernelCofork (hg : S.g = 0) (c : CokernelCofork S.f) (hc : IsColimit c) :
    S.HomologyData where
  left := LeftHomologyData.ofIsColimitCokernelCofork S hg c hc
  right := RightHomologyData.ofIsColimitCokernelCofork S hg c hc
  iso := Iso.refl _

/-- When the second map `S.g` is zero, this is the homology data on `S` given by
the chosen `cokernel S.f` -/
@[simps]
noncomputable def ofHasCokernel (hg : S.g = 0) [HasCokernel S.f] :
    S.HomologyData where
  left := LeftHomologyData.ofHasCokernel S hg
  right := RightHomologyData.ofHasCokernel S hg
  iso := Iso.refl _

/-- When both `S.f` and `S.g` are zero, the middle object `S.X‚ÇÇ` gives a homology data on S -/
@[simps]
noncomputable def ofZeros (hf : S.f = 0) (hg : S.g = 0) :
    S.HomologyData where
  left := LeftHomologyData.ofZeros S hf hg
  right := RightHomologyData.ofZeros S hf hg
  iso := Iso.refl _

/-- If `œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ` is a morphism of short complexes such that `œÜ.œÑ‚ÇÅ` is epi, `œÜ.œÑ‚ÇÇ` is an iso
and `œÜ.œÑ‚ÇÉ` is mono, then a homology data for `S‚ÇÅ` induces a homology data for `S‚ÇÇ`.
The inverse construction is `ofEpiOfIsIsoOfMono'`. -/
@[simps]
noncomputable def ofEpiOfIsIsoOfMono (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) (h : HomologyData S‚ÇÅ)
    [Epi œÜ.œÑ‚ÇÅ] [IsIso œÜ.œÑ‚ÇÇ] [Mono œÜ.œÑ‚ÇÉ] : HomologyData S‚ÇÇ where
  left := LeftHomologyData.ofEpiOfIsIsoOfMono œÜ h.left
  right := RightHomologyData.ofEpiOfIsIsoOfMono œÜ h.right
  iso := h.iso

/-- If `œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ` is a morphism of short complexes such that `œÜ.œÑ‚ÇÅ` is epi, `œÜ.œÑ‚ÇÇ` is an iso
and `œÜ.œÑ‚ÇÉ` is mono, then a homology data for `S‚ÇÇ` induces a homology data for `S‚ÇÅ`.
The inverse construction is `ofEpiOfIsIsoOfMono`. -/
@[simps]
noncomputable def ofEpiOfIsIsoOfMono' (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) (h : HomologyData S‚ÇÇ)
    [Epi œÜ.œÑ‚ÇÅ] [IsIso œÜ.œÑ‚ÇÇ] [Mono œÜ.œÑ‚ÇÉ] : HomologyData S‚ÇÅ where
  left := LeftHomologyData.ofEpiOfIsIsoOfMono' œÜ h.left
  right := RightHomologyData.ofEpiOfIsIsoOfMono' œÜ h.right
  iso := h.iso

/-- If `e : S‚ÇÅ ‚âÖ S‚ÇÇ` is an isomorphism of short complexes and `h‚ÇÅ : HomologyData S‚ÇÅ`,
this is the homology data for `S‚ÇÇ` deduced from the isomorphism. -/
@[simps!]
noncomputable def ofIso (e : S‚ÇÅ ‚âÖ S‚ÇÇ) (h : HomologyData S‚ÇÅ) :=
  h.ofEpiOfIsIsoOfMono e.hom

variable {S}

/-- A homology data for a short complex `S` induces a homology data for `S.op`. -/
@[simps]
def op (h : S.HomologyData) : S.op.HomologyData where
  left := h.right.op
  right := h.left.op
  iso := h.iso.op
  comm := Quiver.Hom.unop_inj (by simp)

/-- A homology data for a short complex `S` in the opposite category
induces a homology data for `S.unop`. -/
@[simps]
def unop {S : ShortComplex C·µí·µñ} (h : S.HomologyData) : S.unop.HomologyData where
  left := h.right.unop
  right := h.left.unop
  iso := h.iso.unop
  comm := Quiver.Hom.op_inj (by simp)

end HomologyData

/-- A short complex `S` has homology when there exists a `S.HomologyData` -/
class HasHomology : Prop where
  /-- the condition that there exists a homology data -/
  condition : Nonempty S.HomologyData

/-- A chosen `S.HomologyData` for a short complex `S` that has homology -/
noncomputable def homologyData [HasHomology S] :
  S.HomologyData := HasHomology.condition.some

variable {S}

lemma HasHomology.mk' (h : S.HomologyData) : HasHomology S :=
  ‚ü®Nonempty.intro h‚ü©

instance [HasHomology S] : HasHomology S.op :=
  HasHomology.mk' S.homologyData.op

instance hasLeftHomology_of_hasHomology [S.HasHomology] : S.HasLeftHomology :=
  HasLeftHomology.mk' S.homologyData.left

instance hasRightHomology_of_hasHomology [S.HasHomology] : S.HasRightHomology :=
  HasRightHomology.mk' S.homologyData.right

instance hasHomology_of_hasCokernel {X Y : C} (f : X ‚ü∂ Y) (Z : C) [HasCokernel f] :
    (ShortComplex.mk f (0 : Y ‚ü∂ Z) comp_zero).HasHomology :=
  HasHomology.mk' (HomologyData.ofHasCokernel _ rfl)

instance hasHomology_of_hasKernel {Y Z : C} (g : Y ‚ü∂ Z) (X : C) [HasKernel g] :
    (ShortComplex.mk (0 : X ‚ü∂ Y) g zero_comp).HasHomology :=
  HasHomology.mk' (HomologyData.ofHasKernel _ rfl)

instance hasHomology_of_zeros (X Y Z : C) :
    (ShortComplex.mk (0 : X ‚ü∂ Y) (0 : Y ‚ü∂ Z) zero_comp).HasHomology :=
  HasHomology.mk' (HomologyData.ofZeros _ rfl rfl)

lemma hasHomology_of_epi_of_isIso_of_mono (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) [HasHomology S‚ÇÅ]
    [Epi œÜ.œÑ‚ÇÅ] [IsIso œÜ.œÑ‚ÇÇ] [Mono œÜ.œÑ‚ÇÉ] : HasHomology S‚ÇÇ :=
  HasHomology.mk' (HomologyData.ofEpiOfIsIsoOfMono œÜ S‚ÇÅ.homologyData)

lemma hasHomology_of_epi_of_isIso_of_mono' (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) [HasHomology S‚ÇÇ]
    [Epi œÜ.œÑ‚ÇÅ] [IsIso œÜ.œÑ‚ÇÇ] [Mono œÜ.œÑ‚ÇÉ] : HasHomology S‚ÇÅ :=
  HasHomology.mk' (HomologyData.ofEpiOfIsIsoOfMono' œÜ S‚ÇÇ.homologyData)

lemma hasHomology_of_iso (e : S‚ÇÅ ‚âÖ S‚ÇÇ) [HasHomology S‚ÇÅ] : HasHomology S‚ÇÇ :=
  HasHomology.mk' (HomologyData.ofIso e S‚ÇÅ.homologyData)

namespace HomologyMapData

/-- The homology map data associated to the identity morphism of a short complex. -/
@[simps]
def id (h : S.HomologyData) : HomologyMapData (ùüô S) h h where
  left := LeftHomologyMapData.id h.left
  right := RightHomologyMapData.id h.right

/-- The homology map data associated to the zero morphism between two short complexes. -/
@[simps]
def zero (h‚ÇÅ : S‚ÇÅ.HomologyData) (h‚ÇÇ : S‚ÇÇ.HomologyData) :
    HomologyMapData 0 h‚ÇÅ h‚ÇÇ where
  left := LeftHomologyMapData.zero h‚ÇÅ.left h‚ÇÇ.left
  right := RightHomologyMapData.zero h‚ÇÅ.right h‚ÇÇ.right

/-- The composition of homology map data. -/
@[simps]
def comp {œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ} {œÜ' : S‚ÇÇ ‚ü∂ S‚ÇÉ} {h‚ÇÅ : S‚ÇÅ.HomologyData}
    {h‚ÇÇ : S‚ÇÇ.HomologyData} {h‚ÇÉ : S‚ÇÉ.HomologyData}
    (œà : HomologyMapData œÜ h‚ÇÅ h‚ÇÇ) (œà' : HomologyMapData œÜ' h‚ÇÇ h‚ÇÉ) :
    HomologyMapData (œÜ ‚â´ œÜ') h‚ÇÅ h‚ÇÉ where
  left := œà.left.comp œà'.left
  right := œà.right.comp œà'.right

/-- A homology map data for a morphism of short complexes induces
a homology map data in the opposite category. -/
@[simps]
def op {œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ} {h‚ÇÅ : S‚ÇÅ.HomologyData} {h‚ÇÇ : S‚ÇÇ.HomologyData}
    (œà : HomologyMapData œÜ h‚ÇÅ h‚ÇÇ) :
    HomologyMapData (opMap œÜ) h‚ÇÇ.op h‚ÇÅ.op where
  left := œà.right.op
  right := œà.left.op

/-- A homology map data for a morphism of short complexes in the opposite category
induces a homology map data in the original category. -/
@[simps]
def unop {S‚ÇÅ S‚ÇÇ : ShortComplex C·µí·µñ} {œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ}
    {h‚ÇÅ : S‚ÇÅ.HomologyData} {h‚ÇÇ : S‚ÇÇ.HomologyData}
    (œà : HomologyMapData œÜ h‚ÇÅ h‚ÇÇ) :
    HomologyMapData (unopMap œÜ) h‚ÇÇ.unop h‚ÇÅ.unop where
  left := œà.right.unop
  right := œà.left.unop

/-- When `S‚ÇÅ.f`, `S‚ÇÅ.g`, `S‚ÇÇ.f` and `S‚ÇÇ.g` are all zero, the action on homology of a
morphism `œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ` is given by the action `œÜ.œÑ‚ÇÇ` on the middle objects. -/
@[simps]
def ofZeros (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) (hf‚ÇÅ : S‚ÇÅ.f = 0) (hg‚ÇÅ : S‚ÇÅ.g = 0) (hf‚ÇÇ : S‚ÇÇ.f = 0) (hg‚ÇÇ : S‚ÇÇ.g = 0) :
    HomologyMapData œÜ (HomologyData.ofZeros S‚ÇÅ hf‚ÇÅ hg‚ÇÅ) (HomologyData.ofZeros S‚ÇÇ hf‚ÇÇ hg‚ÇÇ) where
  left := LeftHomologyMapData.ofZeros œÜ hf‚ÇÅ hg‚ÇÅ hf‚ÇÇ hg‚ÇÇ
  right := RightHomologyMapData.ofZeros œÜ hf‚ÇÅ hg‚ÇÅ hf‚ÇÇ hg‚ÇÇ

/-- When `S‚ÇÅ.g` and `S‚ÇÇ.g` are zero and we have chosen colimit cokernel coforks `c‚ÇÅ` and `c‚ÇÇ`
for `S‚ÇÅ.f` and `S‚ÇÇ.f` respectively, the action on homology of a morphism `œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ` of
short complexes is given by the unique morphism `f : c‚ÇÅ.pt ‚ü∂ c‚ÇÇ.pt` such that
`œÜ.œÑ‚ÇÇ ‚â´ c‚ÇÇ.œÄ = c‚ÇÅ.œÄ ‚â´ f`. -/
@[simps]
def ofIsColimitCokernelCofork (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ)
    (hg‚ÇÅ : S‚ÇÅ.g = 0) (c‚ÇÅ : CokernelCofork S‚ÇÅ.f) (hc‚ÇÅ : IsColimit c‚ÇÅ)
    (hg‚ÇÇ : S‚ÇÇ.g = 0) (c‚ÇÇ : CokernelCofork S‚ÇÇ.f) (hc‚ÇÇ : IsColimit c‚ÇÇ) (f : c‚ÇÅ.pt ‚ü∂ c‚ÇÇ.pt)
    (comm : œÜ.œÑ‚ÇÇ ‚â´ c‚ÇÇ.œÄ = c‚ÇÅ.œÄ ‚â´ f) :
    HomologyMapData œÜ (HomologyData.ofIsColimitCokernelCofork S‚ÇÅ hg‚ÇÅ c‚ÇÅ hc‚ÇÅ)
      (HomologyData.ofIsColimitCokernelCofork S‚ÇÇ hg‚ÇÇ c‚ÇÇ hc‚ÇÇ) where
  left := LeftHomologyMapData.ofIsColimitCokernelCofork œÜ hg‚ÇÅ c‚ÇÅ hc‚ÇÅ hg‚ÇÇ c‚ÇÇ hc‚ÇÇ f comm
  right := RightHomologyMapData.ofIsColimitCokernelCofork œÜ hg‚ÇÅ c‚ÇÅ hc‚ÇÅ hg‚ÇÇ c‚ÇÇ hc‚ÇÇ f comm

/-- When `S‚ÇÅ.f` and `S‚ÇÇ.f` are zero and we have chosen limit kernel forks `c‚ÇÅ` and `c‚ÇÇ`
for `S‚ÇÅ.g` and `S‚ÇÇ.g` respectively, the action on homology of a morphism `œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ` of
short complexes is given by the unique morphism `f : c‚ÇÅ.pt ‚ü∂ c‚ÇÇ.pt` such that
`c‚ÇÅ.Œπ ‚â´ œÜ.œÑ‚ÇÇ = f ‚â´ c‚ÇÇ.Œπ`. -/
@[simps]
def ofIsLimitKernelFork (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ)
    (hf‚ÇÅ : S‚ÇÅ.f = 0) (c‚ÇÅ : KernelFork S‚ÇÅ.g) (hc‚ÇÅ : IsLimit c‚ÇÅ)
    (hf‚ÇÇ : S‚ÇÇ.f = 0) (c‚ÇÇ : KernelFork S‚ÇÇ.g) (hc‚ÇÇ : IsLimit c‚ÇÇ) (f : c‚ÇÅ.pt ‚ü∂ c‚ÇÇ.pt)
    (comm : c‚ÇÅ.Œπ ‚â´ œÜ.œÑ‚ÇÇ = f ‚â´ c‚ÇÇ.Œπ) :
    HomologyMapData œÜ (HomologyData.ofIsLimitKernelFork S‚ÇÅ hf‚ÇÅ c‚ÇÅ hc‚ÇÅ)
      (HomologyData.ofIsLimitKernelFork S‚ÇÇ hf‚ÇÇ c‚ÇÇ hc‚ÇÇ) where
  left := LeftHomologyMapData.ofIsLimitKernelFork œÜ hf‚ÇÅ c‚ÇÅ hc‚ÇÅ hf‚ÇÇ c‚ÇÇ hc‚ÇÇ f comm
  right := RightHomologyMapData.ofIsLimitKernelFork œÜ hf‚ÇÅ c‚ÇÅ hc‚ÇÅ hf‚ÇÇ c‚ÇÇ hc‚ÇÇ f comm

/-- When both maps `S.f` and `S.g` of a short complex `S` are zero, this is the homology map
data (for the identity of `S`) which relates the homology data `ofZeros` and
`ofIsColimitCokernelCofork`. -/
def compatibilityOfZerosOfIsColimitCokernelCofork (hf : S.f = 0) (hg : S.g = 0)
    (c : CokernelCofork S.f) (hc : IsColimit c) :
    HomologyMapData (ùüô S) (HomologyData.ofZeros S hf hg)
      (HomologyData.ofIsColimitCokernelCofork S hg c hc) where
  left := LeftHomologyMapData.compatibilityOfZerosOfIsColimitCokernelCofork S hf hg c hc
  right := RightHomologyMapData.compatibilityOfZerosOfIsColimitCokernelCofork S hf hg c hc

/-- When both maps `S.f` and `S.g` of a short complex `S` are zero, this is the homology map
data (for the identity of `S`) which relates the homology data
`HomologyData.ofIsLimitKernelFork` and `ofZeros` . -/
@[simps]
def compatibilityOfZerosOfIsLimitKernelFork (hf : S.f = 0) (hg : S.g = 0)
    (c : KernelFork S.g) (hc : IsLimit c) :
    HomologyMapData (ùüô S)
      (HomologyData.ofIsLimitKernelFork S hf c hc)
      (HomologyData.ofZeros S hf hg) where
  left := LeftHomologyMapData.compatibilityOfZerosOfIsLimitKernelFork S hf hg c hc
  right := RightHomologyMapData.compatibilityOfZerosOfIsLimitKernelFork S hf hg c hc

/-- This homology map data expresses compatibilities of the homology data
constructed by `HomologyData.ofEpiOfIsIsoOfMono` -/
noncomputable def ofEpiOfIsIsoOfMono (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) (h : HomologyData S‚ÇÅ)
    [Epi œÜ.œÑ‚ÇÅ] [IsIso œÜ.œÑ‚ÇÇ] [Mono œÜ.œÑ‚ÇÉ] :
    HomologyMapData œÜ h (HomologyData.ofEpiOfIsIsoOfMono œÜ h) where
  left := LeftHomologyMapData.ofEpiOfIsIsoOfMono œÜ h.left
  right := RightHomologyMapData.ofEpiOfIsIsoOfMono œÜ h.right

/-- This homology map data expresses compatibilities of the homology data
constructed by `HomologyData.ofEpiOfIsIsoOfMono'` -/
noncomputable def ofEpiOfIsIsoOfMono' (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) (h : HomologyData S‚ÇÇ)
    [Epi œÜ.œÑ‚ÇÅ] [IsIso œÜ.œÑ‚ÇÇ] [Mono œÜ.œÑ‚ÇÉ] :
    HomologyMapData œÜ (HomologyData.ofEpiOfIsIsoOfMono' œÜ h) h where
  left := LeftHomologyMapData.ofEpiOfIsIsoOfMono' œÜ h.left
  right := RightHomologyMapData.ofEpiOfIsIsoOfMono' œÜ h.right

end HomologyMapData

variable (S)

/-- The homology of a short complex is the `left.H` field of a chosen homology data. -/
noncomputable def homology [HasHomology S] : C := S.homologyData.left.H

/-- When a short complex has homology, this is the canonical isomorphism
`S.leftHomology ‚âÖ S.homology`. -/
noncomputable def leftHomologyIso [S.HasHomology] : S.leftHomology ‚âÖ S.homology :=
  leftHomologyMapIso' (Iso.refl _) _ _

/-- When a short complex has homology, this is the canonical isomorphism
`S.rightHomology ‚âÖ S.homology`. -/
noncomputable def rightHomologyIso [S.HasHomology] : S.rightHomology ‚âÖ S.homology :=
  rightHomologyMapIso' (Iso.refl _) _ _ ‚â™‚â´ S.homologyData.iso.symm

variable {S}

/-- When a short complex has homology, its homology can be computed using
any left homology data. -/
noncomputable def LeftHomologyData.homologyIso (h : S.LeftHomologyData) [S.HasHomology] :
    S.homology ‚âÖ h.H := S.leftHomologyIso.symm ‚â™‚â´ h.leftHomologyIso

/-- When a short complex has homology, its homology can be computed using
any right homology data. -/
noncomputable def RightHomologyData.homologyIso (h : S.RightHomologyData) [S.HasHomology] :
    S.homology ‚âÖ h.H := S.rightHomologyIso.symm ‚â™‚â´ h.rightHomologyIso

variable (S)

@[simp]
lemma LeftHomologyData.homologyIso_leftHomologyData [S.HasHomology] :
    S.leftHomologyData.homologyIso = S.leftHomologyIso.symm := by
  ext
  dsimp [homologyIso, leftHomologyIso, ShortComplex.leftHomologyIso]
  rw [‚Üê leftHomologyMap'_comp, comp_id]

@[simp]
lemma RightHomologyData.homologyIso_rightHomologyData [S.HasHomology] :
    S.rightHomologyData.homologyIso = S.rightHomologyIso.symm := by
  ext
  dsimp [homologyIso, rightHomologyIso]
  erw [rightHomologyMap'_id, comp_id]

variable {S}

/-- Given a morphism `œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ` of short complexes and homology data `h‚ÇÅ` and `h‚ÇÇ`
for `S‚ÇÅ` and `S‚ÇÇ` respectively, this is the induced homology map `h‚ÇÅ.left.H ‚ü∂ h‚ÇÅ.left.H`. -/
def homologyMap' (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) (h‚ÇÅ : S‚ÇÅ.HomologyData) (h‚ÇÇ : S‚ÇÇ.HomologyData) :
    h‚ÇÅ.left.H ‚ü∂ h‚ÇÇ.left.H := leftHomologyMap' œÜ _ _

/-- The homology map `S‚ÇÅ.homology ‚ü∂ S‚ÇÇ.homology` induced by a morphism
`S‚ÇÅ ‚ü∂ S‚ÇÇ` of short complexes. -/
noncomputable def homologyMap (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) [HasHomology S‚ÇÅ] [HasHomology S‚ÇÇ] :
    S‚ÇÅ.homology ‚ü∂ S‚ÇÇ.homology :=
  homologyMap' œÜ _ _

namespace HomologyMapData

variable {œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ} {h‚ÇÅ : S‚ÇÅ.HomologyData} {h‚ÇÇ : S‚ÇÇ.HomologyData}
  (Œ≥ : HomologyMapData œÜ h‚ÇÅ h‚ÇÇ)

lemma homologyMap'_eq : homologyMap' œÜ h‚ÇÅ h‚ÇÇ = Œ≥.left.œÜH :=
  LeftHomologyMapData.congr_œÜH (Subsingleton.elim _ _)

lemma cyclesMap'_eq : cyclesMap' œÜ h‚ÇÅ.left h‚ÇÇ.left = Œ≥.left.œÜK :=
  LeftHomologyMapData.congr_œÜK (Subsingleton.elim _ _)

lemma opcyclesMap'_eq : opcyclesMap' œÜ h‚ÇÅ.right h‚ÇÇ.right = Œ≥.right.œÜQ :=
  RightHomologyMapData.congr_œÜQ (Subsingleton.elim _ _)

end HomologyMapData

namespace LeftHomologyMapData

variable {h‚ÇÅ : S‚ÇÅ.LeftHomologyData} {h‚ÇÇ : S‚ÇÇ.LeftHomologyData}
  (Œ≥ : LeftHomologyMapData œÜ h‚ÇÅ h‚ÇÇ) [S‚ÇÅ.HasHomology] [S‚ÇÇ.HasHomology]

lemma homologyMap_eq :
    homologyMap œÜ = h‚ÇÅ.homologyIso.hom ‚â´ Œ≥.œÜH ‚â´ h‚ÇÇ.homologyIso.inv := by
  dsimp [homologyMap, LeftHomologyData.homologyIso, leftHomologyIso,
    LeftHomologyData.leftHomologyIso, homologyMap']
  simp only [‚Üê Œ≥.leftHomologyMap'_eq, ‚Üê leftHomologyMap'_comp, id_comp, comp_id]

lemma homologyMap_comm :
    homologyMap œÜ ‚â´ h‚ÇÇ.homologyIso.hom = h‚ÇÅ.homologyIso.hom ‚â´ Œ≥.œÜH := by
  simp only [Œ≥.homologyMap_eq, assoc, Iso.inv_hom_id, comp_id]

end LeftHomologyMapData

namespace RightHomologyMapData

variable {h‚ÇÅ : S‚ÇÅ.RightHomologyData} {h‚ÇÇ : S‚ÇÇ.RightHomologyData}
  (Œ≥ : RightHomologyMapData œÜ h‚ÇÅ h‚ÇÇ) [S‚ÇÅ.HasHomology] [S‚ÇÇ.HasHomology]

lemma homologyMap_eq :
    homologyMap œÜ = h‚ÇÅ.homologyIso.hom ‚â´ Œ≥.œÜH ‚â´ h‚ÇÇ.homologyIso.inv := by
  dsimp [homologyMap, homologyMap', RightHomologyData.homologyIso,
    rightHomologyIso, RightHomologyData.rightHomologyIso]
  have Œ≥' : HomologyMapData œÜ S‚ÇÅ.homologyData S‚ÇÇ.homologyData := default
  simp only [‚Üê Œ≥.rightHomologyMap'_eq, assoc, ‚Üê rightHomologyMap'_comp_assoc,
    id_comp, comp_id, Œ≥'.left.leftHomologyMap'_eq, Œ≥'.right.rightHomologyMap'_eq, ‚Üê Œ≥'.comm_assoc,
    Iso.hom_inv_id]

lemma homologyMap_comm :
    homologyMap œÜ ‚â´ h‚ÇÇ.homologyIso.hom = h‚ÇÅ.homologyIso.hom ‚â´ Œ≥.œÜH := by
  simp only [Œ≥.homologyMap_eq, assoc, Iso.inv_hom_id, comp_id]

end RightHomologyMapData

@[simp]
lemma homologyMap'_id (h : S.HomologyData) :
    homologyMap' (ùüô S) h h = ùüô _ :=
  (HomologyMapData.id h).homologyMap'_eq

variable (S)

@[simp]
lemma homologyMap_id [HasHomology S] :
    homologyMap (ùüô S) = ùüô _ :=
  homologyMap'_id _

@[simp]
lemma homologyMap'_zero (h‚ÇÅ : S‚ÇÅ.HomologyData) (h‚ÇÇ : S‚ÇÇ.HomologyData) :
    homologyMap' 0 h‚ÇÅ h‚ÇÇ = 0 :=
  (HomologyMapData.zero h‚ÇÅ h‚ÇÇ).homologyMap'_eq

variable (S‚ÇÅ S‚ÇÇ)

@[simp]
lemma homologyMap_zero [S‚ÇÅ.HasHomology] [S‚ÇÇ.HasHomology] :
    homologyMap (0 : S‚ÇÅ ‚ü∂ S‚ÇÇ) = 0 :=
  homologyMap'_zero _ _

variable {S‚ÇÅ S‚ÇÇ}

lemma homologyMap'_comp (œÜ‚ÇÅ : S‚ÇÅ ‚ü∂ S‚ÇÇ) (œÜ‚ÇÇ : S‚ÇÇ ‚ü∂ S‚ÇÉ)
    (h‚ÇÅ : S‚ÇÅ.HomologyData) (h‚ÇÇ : S‚ÇÇ.HomologyData) (h‚ÇÉ : S‚ÇÉ.HomologyData) :
    homologyMap' (œÜ‚ÇÅ ‚â´ œÜ‚ÇÇ) h‚ÇÅ h‚ÇÉ = homologyMap' œÜ‚ÇÅ h‚ÇÅ h‚ÇÇ ‚â´
      homologyMap' œÜ‚ÇÇ h‚ÇÇ h‚ÇÉ :=
  leftHomologyMap'_comp _ _ _ _ _

@[simp]
lemma homologyMap_comp [HasHomology S‚ÇÅ] [HasHomology S‚ÇÇ] [HasHomology S‚ÇÉ]
    (œÜ‚ÇÅ : S‚ÇÅ ‚ü∂ S‚ÇÇ) (œÜ‚ÇÇ : S‚ÇÇ ‚ü∂ S‚ÇÉ) :
    homologyMap (œÜ‚ÇÅ ‚â´ œÜ‚ÇÇ) = homologyMap œÜ‚ÇÅ ‚â´ homologyMap œÜ‚ÇÇ :=
  homologyMap'_comp _ _ _ _ _

/-- Given an isomorphism `S‚ÇÅ ‚âÖ S‚ÇÇ` of short complexes and homology data `h‚ÇÅ` and `h‚ÇÇ`
for `S‚ÇÅ` and `S‚ÇÇ` respectively, this is the induced homology isomorphism `h‚ÇÅ.left.H ‚âÖ h‚ÇÅ.left.H`. -/
@[simps]
def homologyMapIso' (e : S‚ÇÅ ‚âÖ S‚ÇÇ) (h‚ÇÅ : S‚ÇÅ.HomologyData)
    (h‚ÇÇ : S‚ÇÇ.HomologyData) : h‚ÇÅ.left.H ‚âÖ h‚ÇÇ.left.H where
  hom := homologyMap' e.hom h‚ÇÅ h‚ÇÇ
  inv := homologyMap' e.inv h‚ÇÇ h‚ÇÅ
  hom_inv_id := by rw [‚Üê homologyMap'_comp, e.hom_inv_id, homologyMap'_id]
  inv_hom_id := by rw [‚Üê homologyMap'_comp, e.inv_hom_id, homologyMap'_id]

instance isIso_homologyMap'_of_isIso (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) [IsIso œÜ]
    (h‚ÇÅ : S‚ÇÅ.HomologyData) (h‚ÇÇ : S‚ÇÇ.HomologyData) :
    IsIso (homologyMap' œÜ h‚ÇÅ h‚ÇÇ) :=
  (inferInstance : IsIso (homologyMapIso' (asIso œÜ) h‚ÇÅ h‚ÇÇ).hom)

/-- The homology isomorphism `S‚ÇÅ.homology ‚ü∂ S‚ÇÇ.homology` induced by an isomorphism
`S‚ÇÅ ‚âÖ S‚ÇÇ` of short complexes. -/
@[simps]
noncomputable def homologyMapIso (e : S‚ÇÅ ‚âÖ S‚ÇÇ) [S‚ÇÅ.HasHomology]
    [S‚ÇÇ.HasHomology] : S‚ÇÅ.homology ‚âÖ S‚ÇÇ.homology where
  hom := homologyMap e.hom
  inv := homologyMap e.inv
  hom_inv_id := by rw [‚Üê homologyMap_comp, e.hom_inv_id, homologyMap_id]
  inv_hom_id := by rw [‚Üê homologyMap_comp, e.inv_hom_id, homologyMap_id]

instance isIso_homologyMap_of_iso (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) [IsIso œÜ] [S‚ÇÅ.HasHomology]
    [S‚ÇÇ.HasHomology] :
    IsIso (homologyMap œÜ) :=
  (inferInstance : IsIso (homologyMapIso (asIso œÜ)).hom)

variable {S}

section

variable (h‚ÇÅ : S.LeftHomologyData) (h‚ÇÇ : S.RightHomologyData)

/-- If a short complex `S` has both a left homology data `h‚ÇÅ` and a right homology data `h‚ÇÇ`,
this is the canonical morphism `h‚ÇÅ.H ‚ü∂ h‚ÇÇ.H`. -/
def leftRightHomologyComparison' : h‚ÇÅ.H ‚ü∂ h‚ÇÇ.H :=
  h‚ÇÇ.liftH (h‚ÇÅ.descH (h‚ÇÅ.i ‚â´ h‚ÇÇ.p) (by simp))
    (by rw [‚Üê cancel_epi h‚ÇÅ.œÄ, LeftHomologyData.œÄ_descH_assoc, assoc,
      RightHomologyData.p_g', LeftHomologyData.wi, comp_zero])

lemma leftRightHomologyComparison'_eq_liftH :
    leftRightHomologyComparison' h‚ÇÅ h‚ÇÇ =
      h‚ÇÇ.liftH (h‚ÇÅ.descH (h‚ÇÅ.i ‚â´ h‚ÇÇ.p) (by simp))
        (by rw [‚Üê cancel_epi h‚ÇÅ.œÄ, LeftHomologyData.œÄ_descH_assoc, assoc,
          RightHomologyData.p_g', LeftHomologyData.wi, comp_zero]) := rfl

@[reassoc (attr := simp)]
lemma œÄ_leftRightHomologyComparison'_Œπ :
    h‚ÇÅ.œÄ ‚â´ leftRightHomologyComparison' h‚ÇÅ h‚ÇÇ ‚â´ h‚ÇÇ.Œπ = h‚ÇÅ.i ‚â´ h‚ÇÇ.p :=
  by simp only [leftRightHomologyComparison'_eq_liftH,
    RightHomologyData.liftH_Œπ, LeftHomologyData.œÄ_descH]

lemma leftRightHomologyComparison'_eq_descH :
    leftRightHomologyComparison' h‚ÇÅ h‚ÇÇ =
      h‚ÇÅ.descH (h‚ÇÇ.liftH (h‚ÇÅ.i ‚â´ h‚ÇÇ.p) (by simp))
        (by rw [‚Üê cancel_mono h‚ÇÇ.Œπ, assoc, RightHomologyData.liftH_Œπ,
          LeftHomologyData.f'_i_assoc, RightHomologyData.wp, zero_comp]) := by
  simp only [‚Üê cancel_mono h‚ÇÇ.Œπ, ‚Üê cancel_epi h‚ÇÅ.œÄ, œÄ_leftRightHomologyComparison'_Œπ,
    LeftHomologyData.œÄ_descH_assoc, RightHomologyData.liftH_Œπ]

end

variable (S)

/-- If a short complex `S` has both a left and right homology,
this is the canonical morphism `S.leftHomology ‚ü∂ S.rightHomology`. -/
noncomputable def leftRightHomologyComparison [S.HasLeftHomology] [S.HasRightHomology] :
    S.leftHomology ‚ü∂ S.rightHomology :=
  leftRightHomologyComparison' _ _

@[reassoc (attr := simp)]
lemma œÄ_leftRightHomologyComparison_Œπ [S.HasLeftHomology] [S.HasRightHomology] :
    S.leftHomologyœÄ ‚â´ S.leftRightHomologyComparison ‚â´ S.rightHomologyŒπ =
      S.iCycles ‚â´ S.pOpcycles :=
  œÄ_leftRightHomologyComparison'_Œπ _ _

@[reassoc]
lemma leftRightHomologyComparison'_naturality (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) (h‚ÇÅ : S‚ÇÅ.LeftHomologyData)
    (h‚ÇÇ : S‚ÇÅ.RightHomologyData) (h‚ÇÅ' : S‚ÇÇ.LeftHomologyData) (h‚ÇÇ' : S‚ÇÇ.RightHomologyData) :
    leftHomologyMap' œÜ h‚ÇÅ h‚ÇÅ' ‚â´ leftRightHomologyComparison' h‚ÇÅ' h‚ÇÇ' =
      leftRightHomologyComparison' h‚ÇÅ h‚ÇÇ ‚â´ rightHomologyMap' œÜ h‚ÇÇ h‚ÇÇ' := by
  simp only [‚Üê cancel_epi h‚ÇÅ.œÄ, ‚Üê cancel_mono h‚ÇÇ'.Œπ, assoc,
    leftHomologyœÄ_naturality'_assoc, rightHomologyŒπ_naturality',
    œÄ_leftRightHomologyComparison'_Œπ, œÄ_leftRightHomologyComparison'_Œπ_assoc,
    cyclesMap'_i_assoc, p_opcyclesMap']

variable {S}

lemma leftRightHomologyComparison'_compatibility (h‚ÇÅ h‚ÇÅ' : S.LeftHomologyData)
    (h‚ÇÇ h‚ÇÇ' : S.RightHomologyData) :
    leftRightHomologyComparison' h‚ÇÅ h‚ÇÇ = leftHomologyMap' (ùüô S) h‚ÇÅ h‚ÇÅ' ‚â´
      leftRightHomologyComparison' h‚ÇÅ' h‚ÇÇ' ‚â´ rightHomologyMap' (ùüô S) _ _ := by
  rw [leftRightHomologyComparison'_naturality_assoc (ùüô S) h‚ÇÅ h‚ÇÇ h‚ÇÅ' h‚ÇÇ',
    ‚Üê rightHomologyMap'_comp, comp_id, rightHomologyMap'_id, comp_id]

lemma leftRightHomologyComparison_eq [S.HasLeftHomology] [S.HasRightHomology]
    (h‚ÇÅ : S.LeftHomologyData) (h‚ÇÇ : S.RightHomologyData) :
    S.leftRightHomologyComparison = h‚ÇÅ.leftHomologyIso.hom ‚â´
      leftRightHomologyComparison' h‚ÇÅ h‚ÇÇ ‚â´ h‚ÇÇ.rightHomologyIso.inv :=
  leftRightHomologyComparison'_compatibility _ _ _ _

@[simp]
lemma HomologyData.leftRightHomologyComparison'_eq (h : S.HomologyData) :
    leftRightHomologyComparison' h.left h.right = h.iso.hom := by
  simp only [‚Üê cancel_epi h.left.œÄ, ‚Üê cancel_mono h.right.Œπ,
    œÄ_leftRightHomologyComparison'_Œπ, HomologyData.comm]

instance isIso_leftRightHomologyComparison'_of_homologyData (h : S.HomologyData) :
  IsIso (leftRightHomologyComparison' h.left h.right) := by
    rw [h.leftRightHomologyComparison'_eq]
    infer_instance

instance isIso_leftRightHomologyComparison' [S.HasHomology]
    (h‚ÇÅ : S.LeftHomologyData) (h‚ÇÇ : S.RightHomologyData) :
    IsIso (leftRightHomologyComparison' h‚ÇÅ h‚ÇÇ) := by
  rw [leftRightHomologyComparison'_compatibility h‚ÇÅ S.homologyData.left h‚ÇÇ
    S.homologyData.right]
  infer_instance

instance isIso_leftRightHomologyComparison [S.HasHomology] :
    IsIso S.leftRightHomologyComparison := by
  dsimp only [leftRightHomologyComparison]
  infer_instance

namespace HomologyData

/-- This is the homology data for a short complex `S` that is obtained
from a left homology data `h‚ÇÅ` and a right homology data `h‚ÇÇ` when the comparison
morphism `leftRightHomologyComparison' h‚ÇÅ h‚ÇÇ : h‚ÇÅ.H ‚ü∂ h‚ÇÇ.H` is an isomorphism. -/
@[simps]
noncomputable def ofIsIsoLeftRightHomologyComparison'
    (h‚ÇÅ : S.LeftHomologyData) (h‚ÇÇ : S.RightHomologyData)
    [IsIso (leftRightHomologyComparison' h‚ÇÅ h‚ÇÇ)] :
    S.HomologyData where
  left := h‚ÇÅ
  right := h‚ÇÇ
  iso := asIso (leftRightHomologyComparison' h‚ÇÅ h‚ÇÇ)

end HomologyData

lemma leftRightHomologyComparison'_eq_leftHomologpMap'_comp_iso_hom_comp_rightHomologyMap'
    (h : S.HomologyData) (h‚ÇÅ : S.LeftHomologyData) (h‚ÇÇ : S.RightHomologyData) :
    leftRightHomologyComparison' h‚ÇÅ h‚ÇÇ =
      leftHomologyMap' (ùüô S) h‚ÇÅ h.left ‚â´ h.iso.hom ‚â´ rightHomologyMap' (ùüô S) h.right h‚ÇÇ := by
  simpa only [h.leftRightHomologyComparison'_eq] using
    leftRightHomologyComparison'_compatibility h‚ÇÅ h.left h‚ÇÇ h.right

@[reassoc]
lemma leftRightHomologyComparison'_fac (h‚ÇÅ : S.LeftHomologyData) (h‚ÇÇ : S.RightHomologyData)
    [S.HasHomology] :
    leftRightHomologyComparison' h‚ÇÅ h‚ÇÇ = h‚ÇÅ.homologyIso.inv ‚â´ h‚ÇÇ.homologyIso.hom := by
  rw [leftRightHomologyComparison'_eq_leftHomologpMap'_comp_iso_hom_comp_rightHomologyMap'
    S.homologyData h‚ÇÅ h‚ÇÇ]
  dsimp only [LeftHomologyData.homologyIso, LeftHomologyData.leftHomologyIso,
    Iso.symm, Iso.trans, Iso.refl, leftHomologyMapIso', leftHomologyIso,
    RightHomologyData.homologyIso, RightHomologyData.rightHomologyIso,
    rightHomologyMapIso', rightHomologyIso]
  simp only [assoc, ‚Üê leftHomologyMap'_comp_assoc, id_comp, ‚Üê rightHomologyMap'_comp]

variable (S)

@[reassoc]
lemma leftRightHomologyComparison_fac [S.HasHomology] :
    S.leftRightHomologyComparison = S.leftHomologyIso.hom ‚â´ S.rightHomologyIso.inv := by
  simpa only [LeftHomologyData.homologyIso_leftHomologyData, Iso.symm_inv,
    RightHomologyData.homologyIso_rightHomologyData, Iso.symm_hom] using
      leftRightHomologyComparison'_fac S.leftHomologyData S.rightHomologyData

variable {S}

lemma HomologyData.right_homologyIso_eq_left_homologyIso_trans_iso
    (h : S.HomologyData) [S.HasHomology] :
    h.right.homologyIso = h.left.homologyIso ‚â™‚â´ h.iso := by
  suffices h.iso = h.left.homologyIso.symm ‚â™‚â´ h.right.homologyIso by
    rw [this, Iso.self_symm_id_assoc]
  ext
  dsimp
  rw [‚Üê leftRightHomologyComparison'_fac, leftRightHomologyComparison'_eq]

lemma hasHomology_of_isIso_leftRightHomologyComparison'
    (h‚ÇÅ : S.LeftHomologyData) (h‚ÇÇ : S.RightHomologyData)
    [IsIso (leftRightHomologyComparison' h‚ÇÅ h‚ÇÇ)] :
    S.HasHomology :=
  HasHomology.mk' (HomologyData.ofIsIsoLeftRightHomologyComparison' h‚ÇÅ h‚ÇÇ)

lemma hasHomology_of_isIsoLeftRightHomologyComparison [S.HasLeftHomology]
    [S.HasRightHomology] [h : IsIso S.leftRightHomologyComparison] :
    S.HasHomology := by
  haveI : IsIso (leftRightHomologyComparison' S.leftHomologyData S.rightHomologyData) := h
  exact hasHomology_of_isIso_leftRightHomologyComparison' S.leftHomologyData S.rightHomologyData

section

variable [S‚ÇÅ.HasHomology] [S‚ÇÇ.HasHomology] (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ)

@[reassoc]
lemma LeftHomologyData.leftHomologyIso_hom_naturality
    (h‚ÇÅ : S‚ÇÅ.LeftHomologyData) (h‚ÇÇ : S‚ÇÇ.LeftHomologyData) :
    h‚ÇÅ.homologyIso.hom ‚â´ leftHomologyMap' œÜ h‚ÇÅ h‚ÇÇ =
      homologyMap œÜ ‚â´ h‚ÇÇ.homologyIso.hom := by
  dsimp [homologyIso, ShortComplex.leftHomologyIso, homologyMap, homologyMap', leftHomologyIso]
  simp only [‚Üê leftHomologyMap'_comp, id_comp, comp_id]

@[reassoc]
lemma LeftHomologyData.leftHomologyIso_inv_naturality
    (h‚ÇÅ : S‚ÇÅ.LeftHomologyData) (h‚ÇÇ : S‚ÇÇ.LeftHomologyData) :
    h‚ÇÅ.homologyIso.inv ‚â´ homologyMap œÜ =
      leftHomologyMap' œÜ h‚ÇÅ h‚ÇÇ ‚â´ h‚ÇÇ.homologyIso.inv := by
  dsimp [homologyIso, ShortComplex.leftHomologyIso, homologyMap, homologyMap', leftHomologyIso]
  simp only [‚Üê leftHomologyMap'_comp, id_comp, comp_id]

@[reassoc]
lemma leftHomologyIso_hom_naturality :
    S‚ÇÅ.leftHomologyIso.hom ‚â´ homologyMap œÜ =
      leftHomologyMap œÜ ‚â´ S‚ÇÇ.leftHomologyIso.hom := by
  simpa only [LeftHomologyData.homologyIso_leftHomologyData, Iso.symm_inv] using
    LeftHomologyData.leftHomologyIso_inv_naturality œÜ S‚ÇÅ.leftHomologyData S‚ÇÇ.leftHomologyData

@[reassoc]
lemma leftHomologyIso_inv_naturality :
    S‚ÇÅ.leftHomologyIso.inv ‚â´ leftHomologyMap œÜ =
      homologyMap œÜ ‚â´ S‚ÇÇ.leftHomologyIso.inv := by
  simpa only [LeftHomologyData.homologyIso_leftHomologyData, Iso.symm_inv] using
    LeftHomologyData.leftHomologyIso_hom_naturality œÜ S‚ÇÅ.leftHomologyData S‚ÇÇ.leftHomologyData

@[reassoc]
lemma RightHomologyData.rightHomologyIso_hom_naturality
    (h‚ÇÅ : S‚ÇÅ.RightHomologyData) (h‚ÇÇ : S‚ÇÇ.RightHomologyData) :
    h‚ÇÅ.homologyIso.hom ‚â´ rightHomologyMap' œÜ h‚ÇÅ h‚ÇÇ =
      homologyMap œÜ ‚â´ h‚ÇÇ.homologyIso.hom := by
  rw [‚Üê cancel_epi h‚ÇÅ.homologyIso.inv, Iso.inv_hom_id_assoc,
    ‚Üê cancel_epi (leftRightHomologyComparison' S‚ÇÅ.leftHomologyData h‚ÇÅ),
    ‚Üê leftRightHomologyComparison'_naturality œÜ S‚ÇÅ.leftHomologyData h‚ÇÅ S‚ÇÇ.leftHomologyData h‚ÇÇ,
    ‚Üê cancel_epi (S‚ÇÅ.leftHomologyData.homologyIso.hom),
    LeftHomologyData.leftHomologyIso_hom_naturality_assoc,
    leftRightHomologyComparison'_fac, leftRightHomologyComparison'_fac, assoc,
    Iso.hom_inv_id_assoc, Iso.hom_inv_id_assoc, Iso.hom_inv_id_assoc]

@[reassoc]
lemma RightHomologyData.rightHomologyIso_inv_naturality
    (h‚ÇÅ : S‚ÇÅ.RightHomologyData) (h‚ÇÇ : S‚ÇÇ.RightHomologyData) :
      h‚ÇÅ.homologyIso.inv ‚â´ homologyMap œÜ =
        rightHomologyMap' œÜ h‚ÇÅ h‚ÇÇ ‚â´ h‚ÇÇ.homologyIso.inv := by
  simp only [‚Üê cancel_mono h‚ÇÇ.homologyIso.hom, assoc, Iso.inv_hom_id_assoc, comp_id,
    ‚Üê RightHomologyData.rightHomologyIso_hom_naturality œÜ h‚ÇÅ h‚ÇÇ, Iso.inv_hom_id]

@[reassoc]
lemma rightHomologyIso_hom_naturality :
    S‚ÇÅ.rightHomologyIso.hom ‚â´ homologyMap œÜ =
      rightHomologyMap œÜ ‚â´ S‚ÇÇ.rightHomologyIso.hom := by
  simpa only [RightHomologyData.homologyIso_rightHomologyData, Iso.symm_inv] using
    RightHomologyData.rightHomologyIso_inv_naturality œÜ S‚ÇÅ.rightHomologyData S‚ÇÇ.rightHomologyData

@[reassoc]
lemma rightHomologyIso_inv_naturality :
    S‚ÇÅ.rightHomologyIso.inv ‚â´ rightHomologyMap œÜ =
      homologyMap œÜ ‚â´ S‚ÇÇ.rightHomologyIso.inv := by
  simpa only [RightHomologyData.homologyIso_rightHomologyData, Iso.symm_inv] using
    RightHomologyData.rightHomologyIso_hom_naturality œÜ S‚ÇÅ.rightHomologyData S‚ÇÇ.rightHomologyData

end

variable (C)

/-- We shall say that a category `C` is a category with homology when all short complexes
have homology. -/
class _root_.CategoryTheory.CategoryWithHomology : Prop where
  hasHomology : ‚àÄ (S : ShortComplex C), S.HasHomology

attribute [instance] CategoryWithHomology.hasHomology

instance [CategoryWithHomology C] : CategoryWithHomology C·µí·µñ :=
  ‚ü®fun S => HasHomology.mk' S.unop.homologyData.op‚ü©

/-- The homology functor `ShortComplex C ‚•§ C` for a category `C` with homology. -/
@[simps]
noncomputable def homologyFunctor [CategoryWithHomology C] :
    ShortComplex C ‚•§ C where
  obj S := S.homology
  map f := homologyMap f

variable {C}

instance isIso_homologyMap'_of_epi_of_isIso_of_mono (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ)
    (h‚ÇÅ : S‚ÇÅ.HomologyData) (h‚ÇÇ : S‚ÇÇ.HomologyData) [Epi œÜ.œÑ‚ÇÅ] [IsIso œÜ.œÑ‚ÇÇ] [Mono œÜ.œÑ‚ÇÉ] :
    IsIso (homologyMap' œÜ h‚ÇÅ h‚ÇÇ) := by
  dsimp only [homologyMap']
  infer_instance

lemma isIso_homologyMap_of_epi_of_isIso_of_mono' (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) [S‚ÇÅ.HasHomology] [S‚ÇÇ.HasHomology]
    (h‚ÇÅ : Epi œÜ.œÑ‚ÇÅ) (h‚ÇÇ : IsIso œÜ.œÑ‚ÇÇ) (h‚ÇÉ : Mono œÜ.œÑ‚ÇÉ) :
    IsIso (homologyMap œÜ) := by
  dsimp only [homologyMap]
  infer_instance

instance isIso_homologyMap_of_epi_of_isIso_of_mono (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) [S‚ÇÅ.HasHomology] [S‚ÇÇ.HasHomology]
    [Epi œÜ.œÑ‚ÇÅ] [IsIso œÜ.œÑ‚ÇÇ] [Mono œÜ.œÑ‚ÇÉ] :
    IsIso (homologyMap œÜ) :=
  isIso_homologyMap_of_epi_of_isIso_of_mono' œÜ inferInstance inferInstance inferInstance

instance isIso_homologyFunctor_map_of_epi_of_isIso_of_mono (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) [CategoryWithHomology C]
    [Epi œÜ.œÑ‚ÇÅ] [IsIso œÜ.œÑ‚ÇÇ] [Mono œÜ.œÑ‚ÇÉ] :
    IsIso ((homologyFunctor C).map œÜ) :=
  (inferInstance : IsIso (homologyMap œÜ))

instance isIso_homologyMap_of_isIso (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) [S‚ÇÅ.HasHomology] [S‚ÇÇ.HasHomology] [IsIso œÜ] :
    IsIso (homologyMap œÜ) := by
  dsimp only [homologyMap, homologyMap']
  infer_instance

section

variable (S) {A : C}
variable [HasHomology S]

/-- The canonical morphism `S.cycles ‚ü∂ S.homology` for a short complex `S` that has homology. -/
noncomputable def homologyœÄ : S.cycles ‚ü∂ S.homology :=
  S.leftHomologyœÄ ‚â´ S.leftHomologyIso.hom

/-- The canonical morphism `S.homology ‚ü∂ S.opcycles` for a short complex `S` that has homology. -/
noncomputable def homologyŒπ : S.homology ‚ü∂ S.opcycles :=
  S.rightHomologyIso.inv ‚â´ S.rightHomologyŒπ

@[reassoc (attr := simp)]
lemma homologyœÄ_comp_leftHomologyIso_inv:
    S.homologyœÄ ‚â´ S.leftHomologyIso.inv = S.leftHomologyœÄ := by
  dsimp only [homologyœÄ]
  simp only [assoc, Iso.hom_inv_id, comp_id]

@[reassoc (attr := simp)]
lemma rightHomologyIso_hom_comp_homologyŒπ :
    S.rightHomologyIso.hom ‚â´ S.homologyŒπ = S.rightHomologyŒπ := by
  dsimp only [homologyŒπ]
  simp only [Iso.hom_inv_id_assoc]

@[reassoc (attr := simp)]
lemma toCycles_comp_homologyœÄ :
    S.toCycles ‚â´ S.homologyœÄ = 0 := by
  dsimp only [homologyœÄ]
  simp only [toCycles_comp_leftHomologyœÄ_assoc, zero_comp]

@[reassoc (attr := simp)]
lemma homologyŒπ_comp_fromOpcycles :
    S.homologyŒπ ‚â´ S.fromOpcycles = 0 := by
  dsimp only [homologyŒπ]
  simp only [assoc, rightHomologyŒπ_comp_fromOpcycles, comp_zero]

/-- The homology `S.homology` of a short complex is
the cokernel of the morphism `S.toCycles : S.X‚ÇÅ ‚ü∂ S.cycles`. -/
noncomputable def homologyIsCokernel :
    IsColimit (CokernelCofork.ofœÄ S.homologyœÄ S.toCycles_comp_homologyœÄ) :=
  IsColimit.ofIsoColimit S.leftHomologyIsCokernel
    (Cofork.ext S.leftHomologyIso rfl)

/-- The homology `S.homology` of a short complex is
the kernel of the morphism `S.fromOpcycles : S.opcycles ‚ü∂ S.X‚ÇÉ`. -/
noncomputable def homologyIsKernel :
    IsLimit (KernelFork.ofŒπ S.homologyŒπ S.homologyŒπ_comp_fromOpcycles) :=
  IsLimit.ofIsoLimit S.rightHomologyIsKernel
    (Fork.ext S.rightHomologyIso (by simp))

instance : Epi S.homologyœÄ :=
  Limits.epi_of_isColimit_cofork (S.homologyIsCokernel)

instance : Mono S.homologyŒπ :=
  Limits.mono_of_isLimit_fork (S.homologyIsKernel)

/-- Given a morphism `k : S.cycles ‚ü∂ A` such that `S.toCycles ‚â´ k = 0`, this is the
induced morphism `S.homology ‚ü∂ A`. -/
noncomputable def descHomology (k : S.cycles ‚ü∂ A) (hk : S.toCycles ‚â´ k = 0) :
    S.homology ‚ü∂ A :=
  S.homologyIsCokernel.desc (CokernelCofork.ofœÄ k hk)

/-- Given a morphism `k : A ‚ü∂ S.opcycles` such that `k ‚â´ S.fromOpcycles = 0`, this is the
induced morphism `A ‚ü∂ S.homology`. -/
noncomputable def liftHomology (k : A ‚ü∂ S.opcycles) (hk : k ‚â´ S.fromOpcycles = 0) :
    A ‚ü∂ S.homology :=
  S.homologyIsKernel.lift (KernelFork.ofŒπ k hk)

@[reassoc (attr := simp)]
lemma œÄ_descHomology (k : S.cycles ‚ü∂ A) (hk : S.toCycles ‚â´ k = 0) :
    S.homologyœÄ ‚â´ S.descHomology k hk = k :=
  Cofork.IsColimit.œÄ_desc S.homologyIsCokernel

@[reassoc (attr := simp)]
lemma liftHomology_Œπ (k : A ‚ü∂ S.opcycles) (hk : k ‚â´ S.fromOpcycles = 0) :
    S.liftHomology k hk ‚â´ S.homologyŒπ = k :=
  Fork.IsLimit.lift_Œπ S.homologyIsKernel

@[reassoc (attr := simp)]
lemma homologyœÄ_naturality (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) [S‚ÇÅ.HasHomology] [S‚ÇÇ.HasHomology] :
    S‚ÇÅ.homologyœÄ ‚â´ homologyMap œÜ = cyclesMap œÜ ‚â´ S‚ÇÇ.homologyœÄ := by
  simp only [‚Üê cancel_mono S‚ÇÇ.leftHomologyIso.inv, assoc, ‚Üê leftHomologyIso_inv_naturality œÜ,
    homologyœÄ_comp_leftHomologyIso_inv]
  simp only [homologyœÄ, assoc, Iso.hom_inv_id_assoc, leftHomologyœÄ_naturality]

@[reassoc (attr := simp)]
lemma homologyŒπ_naturality (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) [S‚ÇÅ.HasHomology] [S‚ÇÇ.HasHomology] :
    homologyMap œÜ ‚â´ S‚ÇÇ.homologyŒπ = S‚ÇÅ.homologyŒπ ‚â´ S‚ÇÅ.opcyclesMap œÜ  := by
  simp only [‚Üê cancel_epi S‚ÇÅ.rightHomologyIso.hom, rightHomologyIso_hom_naturality_assoc œÜ,
    rightHomologyIso_hom_comp_homologyŒπ, rightHomologyŒπ_naturality]
  simp only [homologyŒπ, assoc, Iso.hom_inv_id_assoc]

@[reassoc (attr := simp)]
lemma homology_œÄ_Œπ :
    S.homologyœÄ ‚â´ S.homologyŒπ = S.iCycles ‚â´ S.pOpcycles := by
  dsimp only [homologyœÄ, homologyŒπ]
  simpa only [assoc, S.leftRightHomologyComparison_fac] using S.œÄ_leftRightHomologyComparison_Œπ

/-- The homology of a short complex `S` identifies to the kernel of the induced morphism
`cokernel S.f ‚ü∂ S.X‚ÇÉ`. -/
noncomputable def homologyIsoKernelDesc [S.HasHomology] [HasCokernel S.f]
    [HasKernel (cokernel.desc S.f S.g S.zero)] :
  S.homology ‚âÖ kernel (cokernel.desc S.f S.g S.zero) :=
    S.rightHomologyIso.symm ‚â™‚â´ S.rightHomologyIsoKernelDesc

/-- The homology of a short complex `S` identifies to the cokernel of the induced morphism
`S.X‚ÇÅ ‚ü∂ kernel S.g`. -/
noncomputable def homologyIsoCokernelLift [S.HasHomology] [HasKernel S.g]
    [HasCokernel (kernel.lift S.g S.f S.zero)] :
  S.homology ‚âÖ cokernel (kernel.lift S.g S.f S.zero) :=
    S.leftHomologyIso.symm ‚â™‚â´ S.leftHomologyIsoCokernelLift

@[reassoc (attr := simp)]
lemma LeftHomologyData.homologyœÄ_comp_homologyIso_hom (h : S.LeftHomologyData) [S.HasHomology] :
    S.homologyœÄ ‚â´ h.homologyIso.hom = h.cyclesIso.hom ‚â´ h.œÄ := by
  dsimp only [homologyœÄ, homologyIso]
  simp only [Iso.trans_hom, Iso.symm_hom, assoc, Iso.hom_inv_id_assoc,
    leftHomologyœÄ_comp_leftHomologyIso_hom]

@[reassoc (attr := simp)]
lemma LeftHomologyData.œÄ_comp_homologyIso_inv (h : S.LeftHomologyData) [S.HasHomology] :
    h.œÄ ‚â´ h.homologyIso.inv = h.cyclesIso.inv ‚â´ S.homologyœÄ := by
  dsimp only [homologyœÄ, homologyIso]
  simp only [Iso.trans_inv, Iso.symm_inv, œÄ_comp_leftHomologyIso_inv_assoc]

@[reassoc (attr := simp)]
lemma RightHomologyData.homologyIso_inv_comp_homologyŒπ
    (h : S.RightHomologyData) [S.HasHomology] :
    h.homologyIso.inv ‚â´ S.homologyŒπ = h.Œπ ‚â´ h.opcyclesIso.inv := by
  dsimp only [homologyŒπ, homologyIso]
  simp only [Iso.trans_inv, Iso.symm_inv, assoc, Iso.hom_inv_id_assoc,
    rightHomologyIso_inv_comp_rightHomologyŒπ]

@[reassoc (attr := simp)]
lemma RightHomologyData.homologyIso_hom_comp_Œπ
    (h : S.RightHomologyData) [S.HasHomology] :
    h.homologyIso.hom ‚â´ h.Œπ = S.homologyŒπ ‚â´ h.opcyclesIso.hom := by
  dsimp only [homologyŒπ, homologyIso]
  simp only [Iso.trans_hom, Iso.symm_hom, assoc, rightHomologyIso_hom_comp_Œπ]

@[reassoc (attr := simp)]
lemma LeftHomologyData.homologyIso_hom_comp_leftHomologyIso_inv
    (h : S.LeftHomologyData) [S.HasHomology] :
    h.homologyIso.hom ‚â´ h.leftHomologyIso.inv = S.leftHomologyIso.inv := by
  dsimp only [homologyIso]
  simp only [Iso.trans_hom, Iso.symm_hom, assoc, Iso.hom_inv_id, comp_id]

@[reassoc (attr := simp)]
lemma LeftHomologyData.leftHomologyIso_hom_comp_homologyIso_inv
    (h : S.LeftHomologyData) [S.HasHomology] :
    h.leftHomologyIso.hom ‚â´ h.homologyIso.inv = S.leftHomologyIso.hom := by
  dsimp only [homologyIso]
  simp only [Iso.trans_inv, Iso.symm_inv, Iso.hom_inv_id_assoc]

@[reassoc (attr := simp)]
lemma RightHomologyData.homologyIso_hom_comp_rightHomologyIso_inv
    (h : S.RightHomologyData) [S.HasHomology] :
    h.homologyIso.hom ‚â´ h.rightHomologyIso.inv = S.rightHomologyIso.inv := by
  dsimp only [homologyIso]
  simp only [Iso.trans_hom, Iso.symm_hom, assoc, Iso.hom_inv_id, comp_id]

@[reassoc (attr := simp)]
lemma RightHomologyData.rightHomologyIso_hom_comp_homologyIso_inv
    (h : S.RightHomologyData) [S.HasHomology] :
    h.rightHomologyIso.hom ‚â´ h.homologyIso.inv = S.rightHomologyIso.hom := by
  dsimp only [homologyIso]
  simp only [Iso.trans_inv, Iso.symm_inv, Iso.hom_inv_id_assoc]

@[reassoc]
lemma comp_homologyMap_comp [S‚ÇÅ.HasHomology] [S‚ÇÇ.HasHomology] (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ)
    (h‚ÇÅ : S‚ÇÅ.LeftHomologyData) (h‚ÇÇ : S‚ÇÇ.RightHomologyData) :
    h‚ÇÅ.œÄ ‚â´ h‚ÇÅ.homologyIso.inv ‚â´ homologyMap œÜ ‚â´ h‚ÇÇ.homologyIso.hom ‚â´ h‚ÇÇ.Œπ =
      h‚ÇÅ.i ‚â´ œÜ.œÑ‚ÇÇ ‚â´ h‚ÇÇ.p := by
  dsimp only [LeftHomologyData.homologyIso, RightHomologyData.homologyIso,
    Iso.symm, Iso.trans, Iso.refl, leftHomologyIso, rightHomologyIso,
    leftHomologyMapIso', rightHomologyMapIso',
    LeftHomologyData.cyclesIso, RightHomologyData.opcyclesIso,
    LeftHomologyData.leftHomologyIso, RightHomologyData.rightHomologyIso,
    homologyMap, homologyMap']
  simp only [assoc, rightHomologyŒπ_naturality', rightHomologyŒπ_naturality'_assoc,
    leftHomologyœÄ_naturality'_assoc, HomologyData.comm_assoc, p_opcyclesMap'_assoc,
    id_œÑ‚ÇÇ, p_opcyclesMap', id_comp, cyclesMap'_i_assoc]

@[reassoc]
lemma œÄ_homologyMap_Œπ [S‚ÇÅ.HasHomology] [S‚ÇÇ.HasHomology] (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ) :
    S‚ÇÅ.homologyœÄ ‚â´ homologyMap œÜ ‚â´ S‚ÇÇ.homologyŒπ = S‚ÇÅ.iCycles ‚â´ œÜ.œÑ‚ÇÇ ‚â´ S‚ÇÇ.pOpcycles := by
  simp only [homologyŒπ_naturality, homology_œÄ_Œπ_assoc, p_opcyclesMap]

end

end ShortComplex

end CategoryTheory
