/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import Mathlib.Algebra.Homology.ShortComplex.QuasiIso
import Mathlib.CategoryTheory.Limits.Preserves.Finite
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Kernels

/-!
# Functors which preserves homology

If `F : C â¥¤ D` is a functor between categories with zero morphisms, we shall
say that `F` preserves homology when `F` preserves both kernels and cokernels.
This typeclass is named `[F.PreservesHomology]`, and is automatically
satisfied when `F` preserves both finite limits and finite colimits.

If `S : ShortComplex C` and `[F.PreservesHomology]`, then there is an
isomorphism `S.mapHomologyIso F : (S.map F).homology â‰… F.obj S.homology`, which
is part of the natural isomorphism `homologyFunctorIso F` between the functors
`F.mapShortComplex â‹™ homologyFunctor D` and `homologyFunctor C â‹™ F`.

-/

namespace CategoryTheory

open Category Limits

variable {C D : Type*} [Category C] [Category D] [HasZeroMorphisms C] [HasZeroMorphisms D]

namespace Functor

variable (F : C â¥¤ D)

/-- A functor preserves homology when it preserves both kernels and cokernels. -/
class PreservesHomology (F : C â¥¤ D) [PreservesZeroMorphisms F] where
  /-- the functor preserves kernels -/
  preservesKernels â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y) : PreservesLimit (parallelPair f 0) F := by
    infer_instance
  /-- the functor preserves cokernels -/
  preservesCokernels â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y) : PreservesColimit (parallelPair f 0) F := by
    infer_instance

variable [PreservesZeroMorphisms F]

/-- A functor which preserves homology preserves kernels. -/
def PreservesHomology.preservesKernel [F.PreservesHomology] {X Y : C} (f : X âŸ¶ Y) :
    PreservesLimit (parallelPair f 0) F :=
  PreservesHomology.preservesKernels _

/-- A functor which preserves homology preserves cokernels. -/
def PreservesHomology.preservesCokernel [F.PreservesHomology] {X Y : C} (f : X âŸ¶ Y) :
    PreservesColimit (parallelPair f 0) F :=
  PreservesHomology.preservesCokernels _

noncomputable instance preservesHomologyOfExact
    [PreservesFiniteLimits F] [PreservesFiniteColimits F] :
  F.PreservesHomology where

end Functor

namespace ShortComplex

variable {S Sâ‚ Sâ‚‚ : ShortComplex C}

namespace LeftHomologyData

variable (h : S.LeftHomologyData) (F : C â¥¤ D)

/-- A left homology data `h` of a short complex `S` is preserved by a functor `F` is
`F` preserves the kernel of `S.g : S.Xâ‚‚ âŸ¶ S.Xâ‚ƒ` and the cokernel of `h.f' : S.Xâ‚ âŸ¶ h.K`. -/
class IsPreservedBy [F.PreservesZeroMorphisms] where
  /-- the functor preserves the kernel of `S.g : S.Xâ‚‚ âŸ¶ S.Xâ‚ƒ`. -/
  g : PreservesLimit (parallelPair S.g 0) F
  /-- the functor preserves the cokernel of `h.f' : S.Xâ‚ âŸ¶ h.K`. -/
  f' : PreservesColimit (parallelPair h.f' 0) F

variable [F.PreservesZeroMorphisms]

noncomputable instance isPreservedByOfPreservesHomology [F.PreservesHomology] :
    h.IsPreservedBy F where
  g := Functor.PreservesHomology.preservesKernel _ _
  f' := Functor.PreservesHomology.preservesCokernel _ _

variable [h.IsPreservedBy F]

/-- When a left homology data is preserved by a functor `F`, this functor
preserves the kernel of `S.g : S.Xâ‚‚ âŸ¶ S.Xâ‚ƒ`. -/
def IsPreservedBy.hg : PreservesLimit (parallelPair S.g 0) F :=
  @IsPreservedBy.g _ _ _ _ _ _ _ h F _ _

/-- When a left homology data `h` is preserved by a functor `F`, this functor
preserves the cokernel of `h.f' : S.Xâ‚ âŸ¶ h.K`. -/
def IsPreservedBy.hf' : PreservesColimit (parallelPair h.f' 0) F := IsPreservedBy.f'

/-- When a left homology data `h` of a short complex `S` is preserved by a functor `F`,
this is the induced left homology data `h.map F` for the short complex `S.map F`. -/
@[simps]
noncomputable def map : (S.map F).LeftHomologyData := by
  have := IsPreservedBy.hg h F
  have := IsPreservedBy.hf' h F
  have wi : F.map h.i â‰« F.map S.g = 0 := by rw [â† F.map_comp, h.wi, F.map_zero]
  have hi := KernelFork.mapIsLimit _ h.hi F
  let f' : F.obj S.Xâ‚ âŸ¶ F.obj h.K := hi.lift (KernelFork.ofÎ¹ (S.map F).f (S.map F).zero)
  have hf' : f' = F.map h.f' := Fork.IsLimit.hom_ext hi (by
    rw [Fork.IsLimit.lift_Î¹ hi]
    simp only [KernelFork.map_Î¹, Fork.Î¹_ofÎ¹, map_f, â† F.map_comp, f'_i])
  have wÏ€ : f' â‰« F.map h.Ï€ = 0 := by rw [hf', â† F.map_comp, f'_Ï€, F.map_zero]
  have hÏ€ : IsColimit (CokernelCofork.ofÏ€ (F.map h.Ï€) wÏ€) := by
    let e : parallelPair f' 0 â‰… parallelPair (F.map h.f') 0 :=
      parallelPair.ext (Iso.refl _) (Iso.refl _) (by simpa using hf') (by simp)
    refine IsColimit.precomposeInvEquiv e _
      (IsColimit.ofIsoColimit (CokernelCofork.mapIsColimit _ h.hÏ€' F) ?_)
    exact Cofork.ext (Iso.refl _) (by simp [e])
  exact
    { K := F.obj h.K
      H := F.obj h.H
      i := F.map h.i
      Ï€ := F.map h.Ï€
      wi := wi
      hi := hi
      wÏ€ := wÏ€
      hÏ€ := hÏ€ }

@[simp]
lemma map_f' : (h.map F).f' = F.map h.f' := by
  rw [â† cancel_mono (h.map F).i, f'_i, map_f, map_i, â† F.map_comp, f'_i]

end LeftHomologyData

/-- Given a left homology map data `Ïˆ : LeftHomologyMapData Ï† hâ‚ hâ‚‚` such that
both left homology data `hâ‚` and `hâ‚‚` are preserved by a functor `F`, this is
the induced left homology map data for the morphism `F.mapShortComplex.map Ï†`. -/
@[simps]
def LeftHomologyMapData.map {Ï† : Sâ‚ âŸ¶ Sâ‚‚} {hâ‚ : Sâ‚.LeftHomologyData}
    {hâ‚‚ : Sâ‚‚.LeftHomologyData} (Ïˆ : LeftHomologyMapData Ï† hâ‚ hâ‚‚) (F : C â¥¤ D)
    [F.PreservesZeroMorphisms] [hâ‚.IsPreservedBy F] [hâ‚‚.IsPreservedBy F] :
    LeftHomologyMapData (F.mapShortComplex.map Ï†) (hâ‚.map F) (hâ‚‚.map F) where
  Ï†K := F.map Ïˆ.Ï†K
  Ï†H := F.map Ïˆ.Ï†H
  commi := by simpa only [F.map_comp] using F.congr_map Ïˆ.commi
  commf' := by simpa only [LeftHomologyData.map_f', F.map_comp] using F.congr_map Ïˆ.commf'
  commÏ€ := by simpa only [F.map_comp] using F.congr_map Ïˆ.commÏ€

namespace RightHomologyData

variable (h : S.RightHomologyData) (F : C â¥¤ D)

/-- A right homology data `h` of a short complex `S` is preserved by a functor `F` is
`F` preserves the cokernel of `S.f : S.Xâ‚ âŸ¶ S.Xâ‚‚` and the kernel of `h.g' : h.Q âŸ¶ S.Xâ‚ƒ`. -/
class IsPreservedBy [F.PreservesZeroMorphisms] where
  /-- the functor preserves the cokernel of `S.f : S.Xâ‚ âŸ¶ S.Xâ‚‚`. -/
  f : PreservesColimit (parallelPair S.f 0) F
  /-- the functor preserves the kernel of `h.g' : h.Q âŸ¶ S.Xâ‚ƒ`. -/
  g' : PreservesLimit (parallelPair h.g' 0) F

variable [F.PreservesZeroMorphisms]

noncomputable instance isPreservedByOfPreservesHomology [F.PreservesHomology] :
    h.IsPreservedBy F where
  f := Functor.PreservesHomology.preservesCokernel F _
  g' := Functor.PreservesHomology.preservesKernel F _

variable [h.IsPreservedBy F]

/-- When a right homology data is preserved by a functor `F`, this functor
preserves the cokernel of `S.f : S.Xâ‚ âŸ¶ S.Xâ‚‚`. -/
def IsPreservedBy.hf : PreservesColimit (parallelPair S.f 0) F :=
  @IsPreservedBy.f _ _ _ _ _ _ _ h F _ _

/-- When a right homology data `h` is preserved by a functor `F`, this functor
preserves the kernel of `h.g' : h.Q âŸ¶ S.Xâ‚ƒ`. -/
def IsPreservedBy.hg' : PreservesLimit (parallelPair h.g' 0) F :=
  @IsPreservedBy.g' _ _ _ _ _ _ _ h F _ _

/-- When a right homology data `h` of a short complex `S` is preserved by a functor `F`,
this is the induced right homology data `h.map F` for the short complex `S.map F`. -/
@[simps]
noncomputable def map : (S.map F).RightHomologyData := by
  have := IsPreservedBy.hf h F
  have := IsPreservedBy.hg' h F
  have wp : F.map S.f â‰« F.map h.p = 0 := by rw [â† F.map_comp, h.wp, F.map_zero]
  have hp := CokernelCofork.mapIsColimit _ h.hp F
  let g' : F.obj h.Q âŸ¶ F.obj S.Xâ‚ƒ := hp.desc (CokernelCofork.ofÏ€ (S.map F).g (S.map F).zero)
  have hg' : g' = F.map h.g' := by
    apply Cofork.IsColimit.hom_ext hp
    rw [Cofork.IsColimit.Ï€_desc hp]
    simp only [Cofork.Ï€_ofÏ€, CokernelCofork.map_Ï€, map_g, â† F.map_comp, p_g']
  have wÎ¹ : F.map h.Î¹ â‰« g' = 0 := by rw [hg', â† F.map_comp, Î¹_g', F.map_zero]
  have hÎ¹ : IsLimit (KernelFork.ofÎ¹ (F.map h.Î¹) wÎ¹) := by
    let e : parallelPair g' 0 â‰… parallelPair (F.map h.g') 0 :=
      parallelPair.ext (Iso.refl _) (Iso.refl _) (by simpa using hg') (by simp)
    refine IsLimit.postcomposeHomEquiv e _
      (IsLimit.ofIsoLimit (KernelFork.mapIsLimit _ h.hÎ¹' F) ?_)
    exact Fork.ext (Iso.refl _) (by simp [e])
  exact
    { Q := F.obj h.Q
      H := F.obj h.H
      p := F.map h.p
      Î¹ := F.map h.Î¹
      wp := wp
      hp := hp
      wÎ¹ := wÎ¹
      hÎ¹ := hÎ¹ }

@[simp]
lemma map_g' : (h.map F).g' = F.map h.g' := by
  rw [â† cancel_epi (h.map F).p, p_g', map_g, map_p, â† F.map_comp, p_g']

end RightHomologyData

/-- Given a right homology map data `Ïˆ : RightHomologyMapData Ï† hâ‚ hâ‚‚` such that
both right homology data `hâ‚` and `hâ‚‚` are preserved by a functor `F`, this is
the induced right homology map data for the morphism `F.mapShortComplex.map Ï†`. -/
@[simps]
def RightHomologyMapData.map {Ï† : Sâ‚ âŸ¶ Sâ‚‚} {hâ‚ : Sâ‚.RightHomologyData}
    {hâ‚‚ : Sâ‚‚.RightHomologyData} (Ïˆ : RightHomologyMapData Ï† hâ‚ hâ‚‚) (F : C â¥¤ D)
    [F.PreservesZeroMorphisms] [hâ‚.IsPreservedBy F] [hâ‚‚.IsPreservedBy F] :
    RightHomologyMapData (F.mapShortComplex.map Ï†) (hâ‚.map F) (hâ‚‚.map F) where
  Ï†Q := F.map Ïˆ.Ï†Q
  Ï†H := F.map Ïˆ.Ï†H
  commp := by simpa only [F.map_comp] using F.congr_map Ïˆ.commp
  commg' := by simpa only [RightHomologyData.map_g', F.map_comp] using F.congr_map Ïˆ.commg'
  commÎ¹ := by simpa only [F.map_comp] using F.congr_map Ïˆ.commÎ¹

/-- When a homology data `h` of a short complex `S` is such that both `h.left` and
`h.right` are preserved by a functor `F`, this is the induced homology data
`h.map F` for the short complex `S.map F`. -/
@[simps]
noncomputable def HomologyData.map (h : S.HomologyData) (F : C â¥¤ D) [F.PreservesZeroMorphisms]
    [h.left.IsPreservedBy F] [h.right.IsPreservedBy F] :
    (S.map F).HomologyData where
  left := h.left.map F
  right := h.right.map F
  iso := F.mapIso h.iso
  comm := by simpa only [F.map_comp] using F.congr_map h.comm

/-- Given a homology map data `Ïˆ : HomologyMapData Ï† hâ‚ hâ‚‚` such that
`hâ‚.left`, `hâ‚.right`, `hâ‚‚.left` and `hâ‚‚.right` are all preserved by a functor `F`, this is
the induced homology map data for the morphism `F.mapShortComplex.map Ï†`. -/
@[simps]
def HomologyMapData.map {Ï† : Sâ‚ âŸ¶ Sâ‚‚} {hâ‚ : Sâ‚.HomologyData} {hâ‚‚ : Sâ‚‚.HomologyData}
    (Ïˆ : HomologyMapData Ï† hâ‚ hâ‚‚) (F : C â¥¤ D) [F.PreservesZeroMorphisms]
    [hâ‚.left.IsPreservedBy F] [hâ‚.right.IsPreservedBy F]
    [hâ‚‚.left.IsPreservedBy F] [hâ‚‚.right.IsPreservedBy F] :
    HomologyMapData (F.mapShortComplex.map Ï†) (hâ‚.map F) (hâ‚‚.map F) where
  left := Ïˆ.left.map F
  right := Ïˆ.right.map F

end ShortComplex

namespace Functor

variable (F : C â¥¤ D) [PreservesZeroMorphisms F] (S : ShortComplex C) {Sâ‚ Sâ‚‚ : ShortComplex C}

/-- A functor preserves the left homology of a short complex `S` if it preserves all the
left homology data of `S`. -/
class PreservesLeftHomologyOf where
  /-- the functor preserves all the left homology data of the short complex -/
  isPreservedBy : âˆ€ (h : S.LeftHomologyData), h.IsPreservedBy F

/-- A functor preserves the right homology of a short complex `S` if it preserves all the
right homology data of `S`. -/
class PreservesRightHomologyOf where
  /-- the functor preserves all the right homology data of the short complex -/
  isPreservedBy : âˆ€ (h : S.RightHomologyData), h.IsPreservedBy F

noncomputable instance PreservesHomology.preservesLeftHomologyOf [F.PreservesHomology] :
    F.PreservesLeftHomologyOf S := âŸ¨inferInstanceâŸ©

noncomputable instance PreservesHomology.preservesRightHomologyOf [F.PreservesHomology] :
    F.PreservesRightHomologyOf S := âŸ¨inferInstanceâŸ©

variable {S}

/-- If a functor preserves a certain left homology data of a short complex `S`, then it
preserves the left homology of `S`. -/
def PreservesLeftHomologyOf.mk' (h : S.LeftHomologyData) [h.IsPreservedBy F] :
    F.PreservesLeftHomologyOf S where
  isPreservedBy h' :=
    { g := ShortComplex.LeftHomologyData.IsPreservedBy.hg h F
      f' := by
        have := ShortComplex.LeftHomologyData.IsPreservedBy.hf' h F
        let e : parallelPair h.f' 0 â‰… parallelPair h'.f' 0 :=
          parallelPair.ext (Iso.refl _) (ShortComplex.cyclesMapIso' (Iso.refl S) h h')
            (by simp) (by simp)
        exact preservesColimitOfIsoDiagram F e }

/-- If a functor preserves a certain right homology data of a short complex `S`, then it
preserves the right homology of `S`. -/
def PreservesRightHomologyOf.mk' (h : S.RightHomologyData) [h.IsPreservedBy F] :
    F.PreservesRightHomologyOf S where
  isPreservedBy h' :=
    { f := ShortComplex.RightHomologyData.IsPreservedBy.hf h F
      g' := by
        have := ShortComplex.RightHomologyData.IsPreservedBy.hg' h F
        let e : parallelPair h.g' 0 â‰… parallelPair h'.g' 0 :=
          parallelPair.ext (ShortComplex.opcyclesMapIso' (Iso.refl S) h h') (Iso.refl _)
            (by simp) (by simp)
        exact preservesLimitOfIsoDiagram F e }

end Functor

namespace ShortComplex

variable {S : ShortComplex C} (hâ‚ : S.LeftHomologyData) (hâ‚‚ : S.RightHomologyData)
  (F : C â¥¤ D) [F.PreservesZeroMorphisms]

instance LeftHomologyData.isPreservedByOfPreserves [F.PreservesLeftHomologyOf S] :
    hâ‚.IsPreservedBy F :=
  Functor.PreservesLeftHomologyOf.isPreservedBy _

instance RightHomologyData.isPreservedByOfPreserves [F.PreservesRightHomologyOf S] :
    hâ‚‚.IsPreservedBy F :=
  Functor.PreservesRightHomologyOf.isPreservedBy _

variable (S)

instance hasLeftHomology_of_preserves [S.HasLeftHomology] [F.PreservesLeftHomologyOf S] :
    (S.map F).HasLeftHomology :=
  HasLeftHomology.mk' (S.leftHomologyData.map F)

instance hasLeftHomology_of_preserves' [S.HasLeftHomology] [F.PreservesLeftHomologyOf S] :
    (F.mapShortComplex.obj S).HasLeftHomology := by
  dsimp; infer_instance

instance hasRightHomology_of_preserves [S.HasRightHomology] [F.PreservesRightHomologyOf S] :
    (S.map F).HasRightHomology :=
  HasRightHomology.mk' (S.rightHomologyData.map F)

instance hasRightHomology_of_preserves' [S.HasRightHomology] [F.PreservesRightHomologyOf S] :
    (F.mapShortComplex.obj S).HasRightHomology := by
  dsimp; infer_instance

instance hasHomology_of_preserves [S.HasHomology] [F.PreservesLeftHomologyOf S]
    [F.PreservesRightHomologyOf S] :
    (S.map F).HasHomology :=
  HasHomology.mk' (S.homologyData.map F)

instance hasHomology_of_preserves' [S.HasHomology] [F.PreservesLeftHomologyOf S]
    [F.PreservesRightHomologyOf S] :
    (F.mapShortComplex.obj S).HasHomology := by
  dsimp; infer_instance

section

variable
  (hl : S.LeftHomologyData) (hr : S.RightHomologyData)
  {Sâ‚ Sâ‚‚ : ShortComplex C} (Ï† : Sâ‚ âŸ¶ Sâ‚‚)
  (hlâ‚ : Sâ‚.LeftHomologyData) (hrâ‚ : Sâ‚.RightHomologyData)
  (hlâ‚‚ : Sâ‚‚.LeftHomologyData) (hrâ‚‚ : Sâ‚‚.RightHomologyData)
  (hâ‚ : Sâ‚.HomologyData) (hâ‚‚ : Sâ‚‚.HomologyData)
  (F : C â¥¤ D) [F.PreservesZeroMorphisms]

namespace LeftHomologyData

variable [hlâ‚.IsPreservedBy F] [hlâ‚‚.IsPreservedBy F]

lemma map_cyclesMap' : F.map (ShortComplex.cyclesMap' Ï† hlâ‚ hlâ‚‚) =
    ShortComplex.cyclesMap' (F.mapShortComplex.map Ï†) (hlâ‚.map F) (hlâ‚‚.map F) := by
  have Î³ : ShortComplex.LeftHomologyMapData Ï† hlâ‚ hlâ‚‚ := default
  rw [Î³.cyclesMap'_eq, (Î³.map F).cyclesMap'_eq,  ShortComplex.LeftHomologyMapData.map_Ï†K]

lemma map_leftHomologyMap' : F.map (ShortComplex.leftHomologyMap' Ï† hlâ‚ hlâ‚‚) =
    ShortComplex.leftHomologyMap' (F.mapShortComplex.map Ï†) (hlâ‚.map F) (hlâ‚‚.map F) := by
  have Î³ : ShortComplex.LeftHomologyMapData Ï† hlâ‚ hlâ‚‚ := default
  rw [Î³.leftHomologyMap'_eq, (Î³.map F).leftHomologyMap'_eq,
    ShortComplex.LeftHomologyMapData.map_Ï†H]

end LeftHomologyData

namespace RightHomologyData

variable [hrâ‚.IsPreservedBy F] [hrâ‚‚.IsPreservedBy F]

lemma map_opcyclesMap' : F.map (ShortComplex.opcyclesMap' Ï† hrâ‚ hrâ‚‚) =
    ShortComplex.opcyclesMap' (F.mapShortComplex.map Ï†) (hrâ‚.map F) (hrâ‚‚.map F) := by
  have Î³ : ShortComplex.RightHomologyMapData Ï† hrâ‚ hrâ‚‚ := default
  rw [Î³.opcyclesMap'_eq, (Î³.map F).opcyclesMap'_eq,  ShortComplex.RightHomologyMapData.map_Ï†Q]

lemma map_rightHomologyMap' : F.map (ShortComplex.rightHomologyMap' Ï† hrâ‚ hrâ‚‚) =
    ShortComplex.rightHomologyMap' (F.mapShortComplex.map Ï†) (hrâ‚.map F) (hrâ‚‚.map F) := by
  have Î³ : ShortComplex.RightHomologyMapData Ï† hrâ‚ hrâ‚‚ := default
  rw [Î³.rightHomologyMap'_eq, (Î³.map F).rightHomologyMap'_eq,
    ShortComplex.RightHomologyMapData.map_Ï†H]

end RightHomologyData

lemma HomologyData.map_homologyMap'
    [hâ‚.left.IsPreservedBy F] [hâ‚.right.IsPreservedBy F]
    [hâ‚‚.left.IsPreservedBy F] [hâ‚‚.right.IsPreservedBy F] :
    F.map (ShortComplex.homologyMap' Ï† hâ‚ hâ‚‚) =
      ShortComplex.homologyMap' (F.mapShortComplex.map Ï†) (hâ‚.map F) (hâ‚‚.map F) :=
  LeftHomologyData.map_leftHomologyMap' _ _ _ _

/-- When a functor `F` preserves the left homology of a short complex `S`, this is the
canonical isomorphism `(S.map F).cycles â‰… F.obj S.cycles`. -/
noncomputable def mapCyclesIso [S.HasLeftHomology] [F.PreservesLeftHomologyOf S] :
    (S.map F).cycles â‰… F.obj S.cycles :=
  (S.leftHomologyData.map F).cyclesIso

@[reassoc (attr := simp)]
lemma mapCyclesIso_hom_iCycles [S.HasLeftHomology] [F.PreservesLeftHomologyOf S] :
    (S.mapCyclesIso F).hom â‰« F.map S.iCycles = (S.map F).iCycles := by
  apply LeftHomologyData.cyclesIso_hom_comp_i

/-- When a functor `F` preserves the left homology of a short complex `S`, this is the
canonical isomorphism `(S.map F).leftHomology â‰… F.obj S.leftHomology`. -/
noncomputable def mapLeftHomologyIso [S.HasLeftHomology] [F.PreservesLeftHomologyOf S] :
    (S.map F).leftHomology â‰… F.obj S.leftHomology :=
  (S.leftHomologyData.map F).leftHomologyIso

/-- When a functor `F` preserves the right homology of a short complex `S`, this is the
canonical isomorphism `(S.map F).opcycles â‰… F.obj S.opcycles`. -/
noncomputable def mapOpcyclesIso [S.HasRightHomology] [F.PreservesRightHomologyOf S] :
    (S.map F).opcycles â‰… F.obj S.opcycles :=
  (S.rightHomologyData.map F).opcyclesIso

/-- When a functor `F` preserves the right homology of a short complex `S`, this is the
canonical isomorphism `(S.map F).rightHomology â‰… F.obj S.rightHomology`. -/
noncomputable def mapRightHomologyIso [S.HasRightHomology] [F.PreservesRightHomologyOf S] :
    (S.map F).rightHomology â‰… F.obj S.rightHomology :=
  (S.rightHomologyData.map F).rightHomologyIso

/-- When a functor `F` preserves the left homology of a short complex `S`, this is the
canonical isomorphism `(S.map F).homology â‰… F.obj S.homology`. -/
noncomputable def mapHomologyIso [S.HasHomology] [(S.map F).HasHomology]
    [F.PreservesLeftHomologyOf S] :
    (S.map F).homology â‰… F.obj S.homology :=
  (S.homologyData.left.map F).homologyIso

/-- When a functor `F` preserves the right homology of a short complex `S`, this is the
canonical isomorphism `(S.map F).homology â‰… F.obj S.homology`. -/
noncomputable def mapHomologyIso' [S.HasHomology] [(S.map F).HasHomology]
    [F.PreservesRightHomologyOf S] :
    (S.map F).homology â‰… F.obj S.homology :=
  (S.homologyData.right.map F).homologyIso â‰ªâ‰« F.mapIso S.homologyData.right.homologyIso.symm

variable {S}

lemma LeftHomologyData.mapCyclesIso_eq [S.HasLeftHomology]
    [F.PreservesLeftHomologyOf S] :
    S.mapCyclesIso F = (hl.map F).cyclesIso â‰ªâ‰« F.mapIso hl.cyclesIso.symm := by
  ext
  dsimp [mapCyclesIso, cyclesIso]
  simp only [map_cyclesMap', â† cyclesMap'_comp, Functor.map_id, comp_id,
    Functor.mapShortComplex_obj]

lemma LeftHomologyData.mapLeftHomologyIso_eq [S.HasLeftHomology]
    [F.PreservesLeftHomologyOf S] :
    S.mapLeftHomologyIso F = (hl.map F).leftHomologyIso â‰ªâ‰« F.mapIso hl.leftHomologyIso.symm := by
  ext
  dsimp [mapLeftHomologyIso, leftHomologyIso]
  simp only [map_leftHomologyMap', â† leftHomologyMap'_comp, Functor.map_id, comp_id,
    Functor.mapShortComplex_obj]

lemma RightHomologyData.mapOpcyclesIso_eq [S.HasRightHomology]
    [F.PreservesRightHomologyOf S] :
    S.mapOpcyclesIso F = (hr.map F).opcyclesIso â‰ªâ‰« F.mapIso hr.opcyclesIso.symm := by
  ext
  dsimp [mapOpcyclesIso, opcyclesIso]
  simp only [map_opcyclesMap', â† opcyclesMap'_comp, Functor.map_id, comp_id,
    Functor.mapShortComplex_obj]

lemma RightHomologyData.mapRightHomologyIso_eq [S.HasRightHomology]
    [F.PreservesRightHomologyOf S] :
    S.mapRightHomologyIso F = (hr.map F).rightHomologyIso â‰ªâ‰«
      F.mapIso hr.rightHomologyIso.symm := by
  ext
  dsimp [mapRightHomologyIso, rightHomologyIso]
  simp only [map_rightHomologyMap', â† rightHomologyMap'_comp, Functor.map_id, comp_id,
    Functor.mapShortComplex_obj]

lemma LeftHomologyData.mapHomologyIso_eq [S.HasHomology]
    [(S.map F).HasHomology] [F.PreservesLeftHomologyOf S] :
    S.mapHomologyIso F = (hl.map F).homologyIso â‰ªâ‰« F.mapIso hl.homologyIso.symm := by
  ext
  dsimp only [mapHomologyIso, homologyIso, ShortComplex.leftHomologyIso,
    leftHomologyMapIso', leftHomologyIso, Functor.mapIso,
    Iso.symm, Iso.trans, Iso.refl]
  simp only [F.map_comp, map_leftHomologyMap', â† leftHomologyMap'_comp, comp_id,
    Functor.map_id, Functor.mapShortComplex_obj]

lemma RightHomologyData.mapHomologyIso'_eq [S.HasHomology]
    [(S.map F).HasHomology] [F.PreservesRightHomologyOf S] :
    S.mapHomologyIso' F = (hr.map F).homologyIso â‰ªâ‰« F.mapIso hr.homologyIso.symm := by
  ext
  dsimp only [Iso.trans, Iso.symm, Iso.refl, Functor.mapIso, mapHomologyIso', homologyIso,
    rightHomologyIso, rightHomologyMapIso', ShortComplex.rightHomologyIso]
  simp only [assoc, F.map_comp, map_rightHomologyMap', â† rightHomologyMap'_comp_assoc]

@[reassoc]
lemma mapCyclesIso_hom_naturality [Sâ‚.HasLeftHomology] [Sâ‚‚.HasLeftHomology]
    [F.PreservesLeftHomologyOf Sâ‚] [F.PreservesLeftHomologyOf Sâ‚‚] :
    cyclesMap (F.mapShortComplex.map Ï†) â‰« (Sâ‚‚.mapCyclesIso F).hom =
      (Sâ‚.mapCyclesIso F).hom â‰« F.map (cyclesMap Ï†) := by
  dsimp only [cyclesMap, mapCyclesIso, LeftHomologyData.cyclesIso, cyclesMapIso', Iso.refl]
  simp only [LeftHomologyData.map_cyclesMap', Functor.mapShortComplex_obj, â† cyclesMap'_comp,
    comp_id, id_comp]

@[reassoc]
lemma mapCyclesIso_inv_naturality [Sâ‚.HasLeftHomology] [Sâ‚‚.HasLeftHomology]
    [F.PreservesLeftHomologyOf Sâ‚] [F.PreservesLeftHomologyOf Sâ‚‚] :
    F.map (cyclesMap Ï†) â‰« (Sâ‚‚.mapCyclesIso F).inv =
      (Sâ‚.mapCyclesIso F).inv â‰« cyclesMap (F.mapShortComplex.map Ï†) := by
  rw [â† cancel_epi (Sâ‚.mapCyclesIso F).hom, â† mapCyclesIso_hom_naturality_assoc,
    Iso.hom_inv_id, comp_id, Iso.hom_inv_id_assoc]

@[reassoc]
lemma mapLeftHomologyIso_hom_naturality [Sâ‚.HasLeftHomology] [Sâ‚‚.HasLeftHomology]
    [F.PreservesLeftHomologyOf Sâ‚] [F.PreservesLeftHomologyOf Sâ‚‚] :
    leftHomologyMap (F.mapShortComplex.map Ï†) â‰« (Sâ‚‚.mapLeftHomologyIso F).hom =
      (Sâ‚.mapLeftHomologyIso F).hom â‰« F.map (leftHomologyMap Ï†) := by
  dsimp only [leftHomologyMap, mapLeftHomologyIso, LeftHomologyData.leftHomologyIso,
    leftHomologyMapIso', Iso.refl]
  simp only [LeftHomologyData.map_leftHomologyMap', Functor.mapShortComplex_obj,
    â† leftHomologyMap'_comp, comp_id, id_comp]

@[reassoc]
lemma mapLeftHomologyIso_inv_naturality [Sâ‚.HasLeftHomology] [Sâ‚‚.HasLeftHomology]
    [F.PreservesLeftHomologyOf Sâ‚] [F.PreservesLeftHomologyOf Sâ‚‚] :
    F.map (leftHomologyMap Ï†) â‰« (Sâ‚‚.mapLeftHomologyIso F).inv =
      (Sâ‚.mapLeftHomologyIso F).inv â‰« leftHomologyMap (F.mapShortComplex.map Ï†) := by
  rw [â† cancel_epi (Sâ‚.mapLeftHomologyIso F).hom, â† mapLeftHomologyIso_hom_naturality_assoc,
    Iso.hom_inv_id, comp_id, Iso.hom_inv_id_assoc]

@[reassoc]
lemma mapOpcyclesIso_hom_naturality [Sâ‚.HasRightHomology] [Sâ‚‚.HasRightHomology]
    [F.PreservesRightHomologyOf Sâ‚] [F.PreservesRightHomologyOf Sâ‚‚] :
    opcyclesMap (F.mapShortComplex.map Ï†) â‰« (Sâ‚‚.mapOpcyclesIso F).hom =
      (Sâ‚.mapOpcyclesIso F).hom â‰« F.map (opcyclesMap Ï†) := by
  dsimp only [opcyclesMap, mapOpcyclesIso, RightHomologyData.opcyclesIso,
    opcyclesMapIso', Iso.refl]
  simp only [RightHomologyData.map_opcyclesMap', Functor.mapShortComplex_obj, â† opcyclesMap'_comp,
    comp_id, id_comp]

@[reassoc]
lemma mapOpcyclesIso_inv_naturality [Sâ‚.HasRightHomology] [Sâ‚‚.HasRightHomology]
    [F.PreservesRightHomologyOf Sâ‚] [F.PreservesRightHomologyOf Sâ‚‚] :
    F.map (opcyclesMap Ï†) â‰« (Sâ‚‚.mapOpcyclesIso F).inv =
      (Sâ‚.mapOpcyclesIso F).inv â‰« opcyclesMap (F.mapShortComplex.map Ï†) := by
  rw [â† cancel_epi (Sâ‚.mapOpcyclesIso F).hom, â† mapOpcyclesIso_hom_naturality_assoc,
    Iso.hom_inv_id, comp_id, Iso.hom_inv_id_assoc]

@[reassoc]
lemma mapRightHomologyIso_hom_naturality [Sâ‚.HasRightHomology] [Sâ‚‚.HasRightHomology]
    [F.PreservesRightHomologyOf Sâ‚] [F.PreservesRightHomologyOf Sâ‚‚] :
    rightHomologyMap (F.mapShortComplex.map Ï†) â‰« (Sâ‚‚.mapRightHomologyIso F).hom =
      (Sâ‚.mapRightHomologyIso F).hom â‰« F.map (rightHomologyMap Ï†) := by
  dsimp only [rightHomologyMap, mapRightHomologyIso, RightHomologyData.rightHomologyIso,
    rightHomologyMapIso', Iso.refl]
  simp only [RightHomologyData.map_rightHomologyMap', Functor.mapShortComplex_obj,
    â† rightHomologyMap'_comp, comp_id, id_comp]

@[reassoc]
lemma mapRightHomologyIso_inv_naturality [Sâ‚.HasRightHomology] [Sâ‚‚.HasRightHomology]
    [F.PreservesRightHomologyOf Sâ‚] [F.PreservesRightHomologyOf Sâ‚‚] :
    F.map (rightHomologyMap Ï†) â‰« (Sâ‚‚.mapRightHomologyIso F).inv =
      (Sâ‚.mapRightHomologyIso F).inv â‰« rightHomologyMap (F.mapShortComplex.map Ï†) := by
  rw [â† cancel_epi (Sâ‚.mapRightHomologyIso F).hom, â† mapRightHomologyIso_hom_naturality_assoc,
    Iso.hom_inv_id, comp_id, Iso.hom_inv_id_assoc]

@[reassoc]
lemma mapHomologyIso_hom_naturality [Sâ‚.HasHomology] [Sâ‚‚.HasHomology]
    [(Sâ‚.map F).HasHomology] [(Sâ‚‚.map F).HasHomology]
    [F.PreservesLeftHomologyOf Sâ‚] [F.PreservesLeftHomologyOf Sâ‚‚] :
    @homologyMap _ _ _ (Sâ‚.map F) (Sâ‚‚.map F) (F.mapShortComplex.map Ï†) _ _ â‰«
      (Sâ‚‚.mapHomologyIso F).hom = (Sâ‚.mapHomologyIso F).hom â‰« F.map (homologyMap Ï†) := by
  dsimp only [homologyMap, homologyMap', mapHomologyIso, LeftHomologyData.homologyIso,
    LeftHomologyData.leftHomologyIso, leftHomologyMapIso', leftHomologyIso,
    Iso.symm, Iso.trans, Iso.refl]
  simp only [LeftHomologyData.map_leftHomologyMap', â† leftHomologyMap'_comp, comp_id, id_comp]

@[reassoc]
lemma mapHomologyIso_inv_naturality [Sâ‚.HasHomology] [Sâ‚‚.HasHomology]
    [(Sâ‚.map F).HasHomology] [(Sâ‚‚.map F).HasHomology]
    [F.PreservesLeftHomologyOf Sâ‚] [F.PreservesLeftHomologyOf Sâ‚‚] :
    F.map (homologyMap Ï†) â‰« (Sâ‚‚.mapHomologyIso F).inv =
      (Sâ‚.mapHomologyIso F).inv â‰«
      @homologyMap _ _ _ (Sâ‚.map F) (Sâ‚‚.map F) (F.mapShortComplex.map Ï†) _ _ := by
  rw [â† cancel_epi (Sâ‚.mapHomologyIso F).hom, â† mapHomologyIso_hom_naturality_assoc,
    Iso.hom_inv_id, comp_id, Iso.hom_inv_id_assoc]

@[reassoc]
lemma mapHomologyIso'_hom_naturality [Sâ‚.HasHomology] [Sâ‚‚.HasHomology]
    [(Sâ‚.map F).HasHomology] [(Sâ‚‚.map F).HasHomology]
    [F.PreservesRightHomologyOf Sâ‚] [F.PreservesRightHomologyOf Sâ‚‚] :
    @homologyMap _ _ _ (Sâ‚.map F) (Sâ‚‚.map F) (F.mapShortComplex.map Ï†) _ _ â‰«
      (Sâ‚‚.mapHomologyIso' F).hom = (Sâ‚.mapHomologyIso' F).hom â‰« F.map (homologyMap Ï†) := by
  dsimp only [Iso.trans, Iso.symm, Functor.mapIso, mapHomologyIso']
  simp only [â† RightHomologyData.rightHomologyIso_hom_naturality_assoc _
    ((homologyData Sâ‚).right.map F) ((homologyData Sâ‚‚).right.map F), assoc,
    â† RightHomologyData.map_rightHomologyMap', â† F.map_comp,
    RightHomologyData.rightHomologyIso_inv_naturality _
      (homologyData Sâ‚).right (homologyData Sâ‚‚).right]

@[reassoc]
lemma mapHomologyIso'_inv_naturality [Sâ‚.HasHomology] [Sâ‚‚.HasHomology]
    [(Sâ‚.map F).HasHomology] [(Sâ‚‚.map F).HasHomology]
    [F.PreservesRightHomologyOf Sâ‚] [F.PreservesRightHomologyOf Sâ‚‚] :
    F.map (homologyMap Ï†) â‰« (Sâ‚‚.mapHomologyIso' F).inv = (Sâ‚.mapHomologyIso' F).inv â‰«
      @homologyMap _ _ _ (Sâ‚.map F) (Sâ‚‚.map F) (F.mapShortComplex.map Ï†) _ _ := by
  rw [â† cancel_epi (Sâ‚.mapHomologyIso' F).hom, â† mapHomologyIso'_hom_naturality_assoc,
    Iso.hom_inv_id, comp_id, Iso.hom_inv_id_assoc]

variable (S)

lemma mapHomologyIso'_eq_mapHomologyIso [S.HasHomology] [F.PreservesLeftHomologyOf S]
    [F.PreservesRightHomologyOf S] :
    S.mapHomologyIso' F = S.mapHomologyIso F := by
  ext
  rw [S.homologyData.left.mapHomologyIso_eq F, S.homologyData.right.mapHomologyIso'_eq F]
  dsimp only [Iso.trans, Iso.symm, Iso.refl, Functor.mapIso, RightHomologyData.homologyIso,
    rightHomologyIso, RightHomologyData.rightHomologyIso, LeftHomologyData.homologyIso,
    leftHomologyIso, LeftHomologyData.leftHomologyIso]
  simp only [RightHomologyData.map_H, rightHomologyMapIso'_inv, rightHomologyMapIso'_hom, assoc,
    Functor.map_comp, RightHomologyData.map_rightHomologyMap', Functor.mapShortComplex_obj,
    Functor.map_id, LeftHomologyData.map_H, leftHomologyMapIso'_inv, leftHomologyMapIso'_hom,
    LeftHomologyData.map_leftHomologyMap', â† rightHomologyMap'_comp_assoc, â† leftHomologyMap'_comp,
    â† leftHomologyMap'_comp_assoc, id_comp]
  have Î³ : HomologyMapData (ðŸ™ (S.map F)) (map S F).homologyData (S.homologyData.map F) := default
  have eq := Î³.comm
  rw [â† Î³.left.leftHomologyMap'_eq, â† Î³.right.rightHomologyMap'_eq] at eq
  dsimp at eq
  simp only [â† reassoc_of% eq, â† F.map_comp, Iso.hom_inv_id, F.map_id, comp_id]

end

section

variable {S}
  {F G : C â¥¤ D} [F.PreservesZeroMorphisms] [G.PreservesZeroMorphisms]
  [F.PreservesLeftHomologyOf S] [G.PreservesLeftHomologyOf S]
  [F.PreservesRightHomologyOf S] [G.PreservesRightHomologyOf S]

/-- Given a natural transformation `Ï„ : F âŸ¶ G` between functors `C â¥¤ D` which preserve
the left homology of a short complex `S`, and a left homology data for `S`,
this is the left homology map data for the morphism `S.mapNatTrans Ï„`
obtained by evaluating `Ï„`. -/
@[simps]
def LeftHomologyMapData.natTransApp (h : LeftHomologyData S) (Ï„ : F âŸ¶ G) :
    LeftHomologyMapData (S.mapNatTrans Ï„) (h.map F) (h.map G) where
  Ï†K := Ï„.app h.K
  Ï†H := Ï„.app h.H

/-- Given a natural transformation `Ï„ : F âŸ¶ G` between functors `C â¥¤ D` which preserve
the right homology of a short complex `S`, and a right homology data for `S`,
this is the right homology map data for the morphism `S.mapNatTrans Ï„`
obtained by evaluating `Ï„`. -/
@[simps]
def RightHomologyMapData.natTransApp (h : RightHomologyData S) (Ï„ : F âŸ¶ G) :
    RightHomologyMapData (S.mapNatTrans Ï„) (h.map F) (h.map G) where
  Ï†Q := Ï„.app h.Q
  Ï†H := Ï„.app h.H

/-- Given a natural transformation `Ï„ : F âŸ¶ G` between functors `C â¥¤ D` which preserve
the homology of a short complex `S`, and a homology data for `S`,
this is the homology map data for the morphism `S.mapNatTrans Ï„`
obtained by evaluating `Ï„`. -/
@[simps]
def HomologyMapData.natTransApp (h : HomologyData S) (Ï„ : F âŸ¶ G) :
    HomologyMapData (S.mapNatTrans Ï„) (h.map F) (h.map G) where
  left := LeftHomologyMapData.natTransApp h.left Ï„
  right := RightHomologyMapData.natTransApp h.right Ï„

variable (S)

lemma homologyMap_mapNatTrans [S.HasHomology] (Ï„ : F âŸ¶ G) :
    homologyMap (S.mapNatTrans Ï„) =
      (S.mapHomologyIso F).hom â‰« Ï„.app S.homology â‰« (S.mapHomologyIso G).inv :=
  (LeftHomologyMapData.natTransApp S.homologyData.left Ï„).homologyMap_eq

end

section

variable [HasKernels C] [HasCokernels C] [HasKernels D] [HasCokernels D]

/-- The natural isomorphism
`F.mapShortComplex â‹™ cyclesFunctor D â‰… cyclesFunctor C â‹™ F`
for a functor `F : C â¥¤ D` which preserves homology. --/
noncomputable def cyclesFunctorIso [F.PreservesHomology] :
    F.mapShortComplex â‹™ ShortComplex.cyclesFunctor D â‰…
      ShortComplex.cyclesFunctor C â‹™ F :=
  NatIso.ofComponents (fun S => S.mapCyclesIso F)
    (fun f => ShortComplex.mapCyclesIso_hom_naturality f F)

/-- The natural isomorphism
`F.mapShortComplex â‹™ leftHomologyFunctor D â‰… leftHomologyFunctor C â‹™ F`
for a functor `F : C â¥¤ D` which preserves homology. --/
noncomputable def leftHomologyFunctorIso [F.PreservesHomology] :
    F.mapShortComplex â‹™ ShortComplex.leftHomologyFunctor D â‰…
      ShortComplex.leftHomologyFunctor C â‹™ F :=
  NatIso.ofComponents (fun S => S.mapLeftHomologyIso F)
    (fun f => ShortComplex.mapLeftHomologyIso_hom_naturality f F)

/-- The natural isomorphism
`F.mapShortComplex â‹™ opcyclesFunctor D â‰… opcyclesFunctor C â‹™ F`
for a functor `F : C â¥¤ D` which preserves homology. --/
noncomputable def opcyclesFunctorIso [F.PreservesHomology] :
    F.mapShortComplex â‹™ ShortComplex.opcyclesFunctor D â‰…
      ShortComplex.opcyclesFunctor C â‹™ F :=
  NatIso.ofComponents (fun S => S.mapOpcyclesIso F)
    (fun f => ShortComplex.mapOpcyclesIso_hom_naturality f F)

/-- The natural isomorphism
`F.mapShortComplex â‹™ rightHomologyFunctor D â‰… rightHomologyFunctor C â‹™ F`
for a functor `F : C â¥¤ D` which preserves homology. --/
noncomputable def rightHomologyFunctorIso [F.PreservesHomology] :
    F.mapShortComplex â‹™ ShortComplex.rightHomologyFunctor D â‰…
      ShortComplex.rightHomologyFunctor C â‹™ F :=
  NatIso.ofComponents (fun S => S.mapRightHomologyIso F)
    (fun f => ShortComplex.mapRightHomologyIso_hom_naturality f F)

end

/-- The natural isomorphism
`F.mapShortComplex â‹™ homologyFunctor D â‰… homologyFunctor C â‹™ F`
for a functor `F : C â¥¤ D` which preserves homology. --/
noncomputable def homologyFunctorIso
    [CategoryWithHomology C] [CategoryWithHomology D] [F.PreservesHomology] :
    F.mapShortComplex â‹™ ShortComplex.homologyFunctor D â‰…
      ShortComplex.homologyFunctor C â‹™ F :=
  NatIso.ofComponents (fun S => S.mapHomologyIso F)
    (fun f => ShortComplex.mapHomologyIso_hom_naturality f F)

section

variable
  {Sâ‚ Sâ‚‚ : ShortComplex C} {Ï† : Sâ‚ âŸ¶ Sâ‚‚}
  {hlâ‚ : Sâ‚.LeftHomologyData} {hrâ‚ : Sâ‚.RightHomologyData}
  {hlâ‚‚ : Sâ‚‚.LeftHomologyData} {hrâ‚‚ : Sâ‚‚.RightHomologyData}
  (Ïˆl : LeftHomologyMapData Ï† hlâ‚ hlâ‚‚)
  (Ïˆr : RightHomologyMapData Ï† hrâ‚ hrâ‚‚)

lemma LeftHomologyMapData.quasiIso_map_iff
    [(F.mapShortComplex.obj Sâ‚).HasHomology]
    [(F.mapShortComplex.obj Sâ‚‚).HasHomology]
    [hlâ‚.IsPreservedBy F] [hlâ‚‚.IsPreservedBy F] :
    QuasiIso (F.mapShortComplex.map Ï†) â†” IsIso (F.map Ïˆl.Ï†H) :=
  (Ïˆl.map F).quasiIso_iff

lemma RightHomologyMapData.quasiIso_map_iff
    [(F.mapShortComplex.obj Sâ‚).HasHomology]
    [(F.mapShortComplex.obj Sâ‚‚).HasHomology]
    [hrâ‚.IsPreservedBy F] [hrâ‚‚.IsPreservedBy F] :
    QuasiIso (F.mapShortComplex.map Ï†) â†” IsIso (F.map Ïˆr.Ï†H) :=
  (Ïˆr.map F).quasiIso_iff

variable (Ï†) [Sâ‚.HasHomology] [Sâ‚‚.HasHomology]
    [(F.mapShortComplex.obj Sâ‚).HasHomology] [(F.mapShortComplex.obj Sâ‚‚).HasHomology]

instance quasiIso_map_of_preservesLeftHomology
    [F.PreservesLeftHomologyOf Sâ‚] [F.PreservesLeftHomologyOf Sâ‚‚]
    [QuasiIso Ï†] : QuasiIso (F.mapShortComplex.map Ï†) := by
  have Î³ : LeftHomologyMapData Ï† Sâ‚.leftHomologyData Sâ‚‚.leftHomologyData := default
  have : IsIso Î³.Ï†H := by
    rw [â† Î³.quasiIso_iff]
    infer_instance
  rw [(Î³.map F).quasiIso_iff, LeftHomologyMapData.map_Ï†H]
  infer_instance

lemma quasiIso_map_iff_of_preservesLeftHomology
    [F.PreservesLeftHomologyOf Sâ‚] [F.PreservesLeftHomologyOf Sâ‚‚]
    [F.ReflectsIsomorphisms] :
    QuasiIso (F.mapShortComplex.map Ï†) â†” QuasiIso Ï† := by
  have Î³ : LeftHomologyMapData Ï† Sâ‚.leftHomologyData Sâ‚‚.leftHomologyData := default
  rw [Î³.quasiIso_iff, (Î³.map F).quasiIso_iff, LeftHomologyMapData.map_Ï†H]
  constructor
  Â· intro
    exact isIso_of_reflects_iso _ F
  Â· intro
    infer_instance

instance quasiIso_map_of_preservesRightHomology
    [F.PreservesRightHomologyOf Sâ‚] [F.PreservesRightHomologyOf Sâ‚‚]
    [QuasiIso Ï†] : QuasiIso (F.mapShortComplex.map Ï†) := by
  have Î³ : RightHomologyMapData Ï† Sâ‚.rightHomologyData Sâ‚‚.rightHomologyData := default
  have : IsIso Î³.Ï†H := by
    rw [â† Î³.quasiIso_iff]
    infer_instance
  rw [(Î³.map F).quasiIso_iff, RightHomologyMapData.map_Ï†H]
  infer_instance

lemma quasiIso_map_iff_of_preservesRightHomology
    [F.PreservesRightHomologyOf Sâ‚] [F.PreservesRightHomologyOf Sâ‚‚]
    [F.ReflectsIsomorphisms] :
    QuasiIso (F.mapShortComplex.map Ï†) â†” QuasiIso Ï† := by
  have Î³ : RightHomologyMapData Ï† Sâ‚.rightHomologyData Sâ‚‚.rightHomologyData := default
  rw [Î³.quasiIso_iff, (Î³.map F).quasiIso_iff, RightHomologyMapData.map_Ï†H]
  constructor
  Â· intro
    exact isIso_of_reflects_iso _ F
  Â· intro
    infer_instance

end

end ShortComplex

namespace Functor

variable (F : C â¥¤ D) [F.PreservesZeroMorphisms] (S : ShortComplex C)

/-- If a short complex `S` is such that `S.f = 0` and that the kernel of `S.g` is preserved
by a functor `F`, then `F` preserves the left homology of `S`. -/
noncomputable def preservesLeftHomologyOfZerof (hf : S.f = 0)
    [PreservesLimit (parallelPair S.g 0) F] :
    F.PreservesLeftHomologyOf S := âŸ¨fun h =>
  { g := by infer_instance
    f' := Limits.preservesCokernelZero' _ _
      (by rw [â† cancel_mono h.i, h.f'_i, zero_comp, hf]) }âŸ©

/-- If a short complex `S` is such that `S.g = 0` and that the cokernel of `S.f` is preserved
by a functor `F`, then `F` preserves the right homology of `S`. -/
noncomputable def preservesRightHomologyOfZerog (hg : S.g = 0)
    [PreservesColimit (parallelPair S.f 0) F] :
    F.PreservesRightHomologyOf S := âŸ¨fun h =>
  { f := by infer_instance
    g' := Limits.preservesKernelZero' _ _
      (by rw [â† cancel_epi h.p, h.p_g', comp_zero, hg]) }âŸ©

/-- If a short complex `S` is such that `S.g = 0` and that the cokernel of `S.f` is preserved
by a functor `F`, then `F` preserves the left homology of `S`. -/
noncomputable def preservesLeftHomologyOfZerog (hg : S.g = 0)
    [PreservesColimit (parallelPair S.f 0) F] :
    F.PreservesLeftHomologyOf S := âŸ¨fun h =>
  { g := by
      rw [hg]
      infer_instance
    f' := by
      have := h.isIso_i hg
      let e : parallelPair h.f' 0 â‰… parallelPair S.f 0 :=
        parallelPair.ext (Iso.refl _) (asIso h.i) (by aesop_cat) (by aesop_cat)
      exact Limits.preservesColimitOfIsoDiagram F e.symm}âŸ©

/-- If a short complex `S` is such that `S.f = 0` and that the kernel of `S.g` is preserved
by a functor `F`, then `F` preserves the right homology of `S`. -/
noncomputable def preservesRightHomologyOfZerof (hf : S.f = 0)
    [PreservesLimit (parallelPair S.g 0) F] :
    F.PreservesRightHomologyOf S := âŸ¨fun h =>
  { f := by
      rw [hf]
      infer_instance
    g' := by
      have := h.isIso_p hf
      let e : parallelPair S.g 0 â‰… parallelPair h.g' 0 :=
        parallelPair.ext (asIso h.p) (Iso.refl _) (by aesop_cat) (by aesop_cat)
      exact Limits.preservesLimitOfIsoDiagram F e }âŸ©

end Functor

lemma NatTrans.app_homology {F G : C â¥¤ D} (Ï„ : F âŸ¶ G)
    (S : ShortComplex C) [S.HasHomology] [F.PreservesZeroMorphisms] [G.PreservesZeroMorphisms]
    [F.PreservesLeftHomologyOf S] [G.PreservesLeftHomologyOf S] [F.PreservesRightHomologyOf S]
    [G.PreservesRightHomologyOf S] :
    Ï„.app S.homology = (S.mapHomologyIso F).inv â‰«
      ShortComplex.homologyMap (S.mapNatTrans Ï„) â‰« (S.mapHomologyIso G).hom := by
  rw [ShortComplex.homologyMap_mapNatTrans, assoc, assoc, Iso.inv_hom_id,
    comp_id, Iso.inv_hom_id_assoc]

end CategoryTheory
