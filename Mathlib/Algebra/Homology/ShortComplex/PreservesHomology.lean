/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/

import Mathlib.Algebra.Homology.ShortComplex.QuasiIso
import Mathlib.CategoryTheory.Limits.Preserves.Finite
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Kernels

/-!
# Functors which preserves homology

If `F : C ‚•§ D` is a functor between categories with zero morphisms, we shall
say that `F` preserves homology when `F` preserves both kernels and cokernels.
This typeclass is named `[F.PreservesHomology]`, and is automatically
satisfied when `F` preserves both finite limits and finite colimits.

If `S : ShortComplex C` and `[F.PreservesHomology]`, then there is an
isomorphism `S.mapHomologyIso F : (S.map F).homology ‚âÖ F.obj S.homology`, which
is part of the natural isomorphism `homologyFunctorIso F` between the functors
`F.mapShortComplex ‚ãô homologyFunctor D` and `homologyFunctor C ‚ãô F`.

-/

namespace CategoryTheory

open Category Limits

variable {C D : Type*} [Category C] [Category D] [HasZeroMorphisms C] [HasZeroMorphisms D]

namespace Functor

variable (F : C ‚•§ D)

/-- A functor preserves homology when it preserves both kernels and cokernels. -/
class PreservesHomology (F : C ‚•§ D) [PreservesZeroMorphisms F] : Prop where
  /-- the functor preserves kernels -/
  preservesKernels ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y) : PreservesLimit (parallelPair f 0) F := by
    infer_instance
  /-- the functor preserves cokernels -/
  preservesCokernels ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y) : PreservesColimit (parallelPair f 0) F := by
    infer_instance

variable [PreservesZeroMorphisms F]

/-- A functor which preserves homology preserves kernels. -/
lemma PreservesHomology.preservesKernel [F.PreservesHomology] {X Y : C} (f : X ‚ü∂ Y) :
    PreservesLimit (parallelPair f 0) F :=
  PreservesHomology.preservesKernels _

/-- A functor which preserves homology preserves cokernels. -/
lemma PreservesHomology.preservesCokernel [F.PreservesHomology] {X Y : C} (f : X ‚ü∂ Y) :
    PreservesColimit (parallelPair f 0) F :=
  PreservesHomology.preservesCokernels _

noncomputable instance preservesHomologyOfExact
    [PreservesFiniteLimits F] [PreservesFiniteColimits F] : F.PreservesHomology where

end Functor

namespace ShortComplex

variable {S S‚ÇÅ S‚ÇÇ : ShortComplex C}

namespace LeftHomologyData

variable (h : S.LeftHomologyData) (F : C ‚•§ D)

/-- A left homology data `h` of a short complex `S` is preserved by a functor `F` is
`F` preserves the kernel of `S.g : S.X‚ÇÇ ‚ü∂ S.X‚ÇÉ` and the cokernel of `h.f' : S.X‚ÇÅ ‚ü∂ h.K`. -/
class IsPreservedBy [F.PreservesZeroMorphisms] : Prop where
  /-- the functor preserves the kernel of `S.g : S.X‚ÇÇ ‚ü∂ S.X‚ÇÉ`. -/
  g : PreservesLimit (parallelPair S.g 0) F
  /-- the functor preserves the cokernel of `h.f' : S.X‚ÇÅ ‚ü∂ h.K`. -/
  f' : PreservesColimit (parallelPair h.f' 0) F

variable [F.PreservesZeroMorphisms]

noncomputable instance isPreservedBy_of_preservesHomology [F.PreservesHomology] :
    h.IsPreservedBy F where
  g := Functor.PreservesHomology.preservesKernel _ _
  f' := Functor.PreservesHomology.preservesCokernel _ _

variable [h.IsPreservedBy F]

include h in
/-- When a left homology data is preserved by a functor `F`, this functor
preserves the kernel of `S.g : S.X‚ÇÇ ‚ü∂ S.X‚ÇÉ`. -/
lemma IsPreservedBy.hg : PreservesLimit (parallelPair S.g 0) F :=
  @IsPreservedBy.g _ _ _ _ _ _ _ h F _ _

/-- When a left homology data `h` is preserved by a functor `F`, this functor
preserves the cokernel of `h.f' : S.X‚ÇÅ ‚ü∂ h.K`. -/
lemma IsPreservedBy.hf' : PreservesColimit (parallelPair h.f' 0) F := IsPreservedBy.f'

/-- When a left homology data `h` of a short complex `S` is preserved by a functor `F`,
this is the induced left homology data `h.map F` for the short complex `S.map F`. -/
@[simps]
noncomputable def map : (S.map F).LeftHomologyData := by
  have := IsPreservedBy.hg h F
  have := IsPreservedBy.hf' h F
  have wi : F.map h.i ‚â´ F.map S.g = 0 := by rw [‚Üê F.map_comp, h.wi, F.map_zero]
  have hi := KernelFork.mapIsLimit _ h.hi F
  let f' : F.obj S.X‚ÇÅ ‚ü∂ F.obj h.K := hi.lift (KernelFork.ofŒπ (S.map F).f (S.map F).zero)
  have hf' : f' = F.map h.f' := Fork.IsLimit.hom_ext hi (by
    rw [Fork.IsLimit.lift_Œπ hi]
    simp only [KernelFork.map_Œπ, Fork.Œπ_ofŒπ, map_f, ‚Üê F.map_comp, f'_i])
  have wœÄ : f' ‚â´ F.map h.œÄ = 0 := by rw [hf', ‚Üê F.map_comp, f'_œÄ, F.map_zero]
  have hœÄ : IsColimit (CokernelCofork.ofœÄ (F.map h.œÄ) wœÄ) := by
    let e : parallelPair f' 0 ‚âÖ parallelPair (F.map h.f') 0 :=
      parallelPair.ext (Iso.refl _) (Iso.refl _) (by simpa using hf') (by simp)
    refine IsColimit.precomposeInvEquiv e _
      (IsColimit.ofIsoColimit (CokernelCofork.mapIsColimit _ h.hœÄ' F) ?_)
    exact Cofork.ext (Iso.refl _) (by simp [e])
  exact
    { K := F.obj h.K
      H := F.obj h.H
      i := F.map h.i
      œÄ := F.map h.œÄ
      wi := wi
      hi := hi
      wœÄ := wœÄ
      hœÄ := hœÄ }

@[simp]
lemma map_f' : (h.map F).f' = F.map h.f' := by
  rw [‚Üê cancel_mono (h.map F).i, f'_i, map_f, map_i, ‚Üê F.map_comp, f'_i]

end LeftHomologyData

/-- Given a left homology map data `œà : LeftHomologyMapData œÜ h‚ÇÅ h‚ÇÇ` such that
both left homology data `h‚ÇÅ` and `h‚ÇÇ` are preserved by a functor `F`, this is
the induced left homology map data for the morphism `F.mapShortComplex.map œÜ`. -/
@[simps]
noncomputable def LeftHomologyMapData.map {œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ} {h‚ÇÅ : S‚ÇÅ.LeftHomologyData}
    {h‚ÇÇ : S‚ÇÇ.LeftHomologyData} (œà : LeftHomologyMapData œÜ h‚ÇÅ h‚ÇÇ) (F : C ‚•§ D)
    [F.PreservesZeroMorphisms] [h‚ÇÅ.IsPreservedBy F] [h‚ÇÇ.IsPreservedBy F] :
    LeftHomologyMapData (F.mapShortComplex.map œÜ) (h‚ÇÅ.map F) (h‚ÇÇ.map F) where
  œÜK := F.map œà.œÜK
  œÜH := F.map œà.œÜH
  commi := by simpa only [F.map_comp] using F.congr_map œà.commi
  commf' := by simpa only [LeftHomologyData.map_f', F.map_comp] using F.congr_map œà.commf'
  commœÄ := by simpa only [F.map_comp] using F.congr_map œà.commœÄ

namespace RightHomologyData

variable (h : S.RightHomologyData) (F : C ‚•§ D)

/-- A right homology data `h` of a short complex `S` is preserved by a functor `F` is
`F` preserves the cokernel of `S.f : S.X‚ÇÅ ‚ü∂ S.X‚ÇÇ` and the kernel of `h.g' : h.Q ‚ü∂ S.X‚ÇÉ`. -/
class IsPreservedBy [F.PreservesZeroMorphisms] : Prop where
  /-- the functor preserves the cokernel of `S.f : S.X‚ÇÅ ‚ü∂ S.X‚ÇÇ`. -/
  f : PreservesColimit (parallelPair S.f 0) F
  /-- the functor preserves the kernel of `h.g' : h.Q ‚ü∂ S.X‚ÇÉ`. -/
  g' : PreservesLimit (parallelPair h.g' 0) F

variable [F.PreservesZeroMorphisms]

noncomputable instance isPreservedBy_of_preservesHomology [F.PreservesHomology] :
    h.IsPreservedBy F where
  f := Functor.PreservesHomology.preservesCokernel F _
  g' := Functor.PreservesHomology.preservesKernel F _

variable [h.IsPreservedBy F]

include h in
/-- When a right homology data is preserved by a functor `F`, this functor
preserves the cokernel of `S.f : S.X‚ÇÅ ‚ü∂ S.X‚ÇÇ`. -/
lemma IsPreservedBy.hf : PreservesColimit (parallelPair S.f 0) F :=
  @IsPreservedBy.f _ _ _ _ _ _ _ h F _ _

/-- When a right homology data `h` is preserved by a functor `F`, this functor
preserves the kernel of `h.g' : h.Q ‚ü∂ S.X‚ÇÉ`. -/
lemma IsPreservedBy.hg' : PreservesLimit (parallelPair h.g' 0) F :=
  @IsPreservedBy.g' _ _ _ _ _ _ _ h F _ _

/-- When a right homology data `h` of a short complex `S` is preserved by a functor `F`,
this is the induced right homology data `h.map F` for the short complex `S.map F`. -/
@[simps]
noncomputable def map : (S.map F).RightHomologyData := by
  have := IsPreservedBy.hf h F
  have := IsPreservedBy.hg' h F
  have wp : F.map S.f ‚â´ F.map h.p = 0 := by rw [‚Üê F.map_comp, h.wp, F.map_zero]
  have hp := CokernelCofork.mapIsColimit _ h.hp F
  let g' : F.obj h.Q ‚ü∂ F.obj S.X‚ÇÉ := hp.desc (CokernelCofork.ofœÄ (S.map F).g (S.map F).zero)
  have hg' : g' = F.map h.g' := by
    apply Cofork.IsColimit.hom_ext hp
    rw [Cofork.IsColimit.œÄ_desc hp]
    simp only [Cofork.œÄ_ofœÄ, CokernelCofork.map_œÄ, map_g, ‚Üê F.map_comp, p_g']
  have wŒπ : F.map h.Œπ ‚â´ g' = 0 := by rw [hg', ‚Üê F.map_comp, Œπ_g', F.map_zero]
  have hŒπ : IsLimit (KernelFork.ofŒπ (F.map h.Œπ) wŒπ) := by
    let e : parallelPair g' 0 ‚âÖ parallelPair (F.map h.g') 0 :=
      parallelPair.ext (Iso.refl _) (Iso.refl _) (by simpa using hg') (by simp)
    refine IsLimit.postcomposeHomEquiv e _
      (IsLimit.ofIsoLimit (KernelFork.mapIsLimit _ h.hŒπ' F) ?_)
    exact Fork.ext (Iso.refl _) (by simp [e])
  exact
    { Q := F.obj h.Q
      H := F.obj h.H
      p := F.map h.p
      Œπ := F.map h.Œπ
      wp := wp
      hp := hp
      wŒπ := wŒπ
      hŒπ := hŒπ }

@[simp]
lemma map_g' : (h.map F).g' = F.map h.g' := by
  rw [‚Üê cancel_epi (h.map F).p, p_g', map_g, map_p, ‚Üê F.map_comp, p_g']

end RightHomologyData

/-- Given a right homology map data `œà : RightHomologyMapData œÜ h‚ÇÅ h‚ÇÇ` such that
both right homology data `h‚ÇÅ` and `h‚ÇÇ` are preserved by a functor `F`, this is
the induced right homology map data for the morphism `F.mapShortComplex.map œÜ`. -/
@[simps]
noncomputable def RightHomologyMapData.map {œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ} {h‚ÇÅ : S‚ÇÅ.RightHomologyData}
    {h‚ÇÇ : S‚ÇÇ.RightHomologyData} (œà : RightHomologyMapData œÜ h‚ÇÅ h‚ÇÇ) (F : C ‚•§ D)
    [F.PreservesZeroMorphisms] [h‚ÇÅ.IsPreservedBy F] [h‚ÇÇ.IsPreservedBy F] :
    RightHomologyMapData (F.mapShortComplex.map œÜ) (h‚ÇÅ.map F) (h‚ÇÇ.map F) where
  œÜQ := F.map œà.œÜQ
  œÜH := F.map œà.œÜH
  commp := by simpa only [F.map_comp] using F.congr_map œà.commp
  commg' := by simpa only [RightHomologyData.map_g', F.map_comp] using F.congr_map œà.commg'
  commŒπ := by simpa only [F.map_comp] using F.congr_map œà.commŒπ

/-- When a homology data `h` of a short complex `S` is such that both `h.left` and
`h.right` are preserved by a functor `F`, this is the induced homology data
`h.map F` for the short complex `S.map F`. -/
@[simps]
noncomputable def HomologyData.map (h : S.HomologyData) (F : C ‚•§ D) [F.PreservesZeroMorphisms]
    [h.left.IsPreservedBy F] [h.right.IsPreservedBy F] :
    (S.map F).HomologyData where
  left := h.left.map F
  right := h.right.map F
  iso := F.mapIso h.iso
  comm := by simpa only [F.map_comp] using F.congr_map h.comm

/-- Given a homology map data `œà : HomologyMapData œÜ h‚ÇÅ h‚ÇÇ` such that
`h‚ÇÅ.left`, `h‚ÇÅ.right`, `h‚ÇÇ.left` and `h‚ÇÇ.right` are all preserved by a functor `F`, this is
the induced homology map data for the morphism `F.mapShortComplex.map œÜ`. -/
@[simps]
noncomputable def HomologyMapData.map {œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ} {h‚ÇÅ : S‚ÇÅ.HomologyData} {h‚ÇÇ : S‚ÇÇ.HomologyData}
    (œà : HomologyMapData œÜ h‚ÇÅ h‚ÇÇ) (F : C ‚•§ D) [F.PreservesZeroMorphisms]
    [h‚ÇÅ.left.IsPreservedBy F] [h‚ÇÅ.right.IsPreservedBy F]
    [h‚ÇÇ.left.IsPreservedBy F] [h‚ÇÇ.right.IsPreservedBy F] :
    HomologyMapData (F.mapShortComplex.map œÜ) (h‚ÇÅ.map F) (h‚ÇÇ.map F) where
  left := œà.left.map F
  right := œà.right.map F

lemma map_leftRightHomologyComparison' (F : C ‚•§ D) [F.PreservesZeroMorphisms]
    (h‚Çó : S.LeftHomologyData) (h·µ£ : S.RightHomologyData) [h‚Çó.IsPreservedBy F] [h·µ£.IsPreservedBy F] :
    F.map (leftRightHomologyComparison' h‚Çó h·µ£) =
      leftRightHomologyComparison' (h‚Çó.map F) (h·µ£.map F) := by
  apply Cofork.IsColimit.hom_ext (h‚Çó.map F).hœÄ
  apply Fork.IsLimit.hom_ext (h·µ£.map F).hŒπ
  trans F.map (h‚Çó.i ‚â´ h·µ£.p)
  ¬∑ simp [‚Üê Functor.map_comp]
  trans (h‚Çó.map F).œÄ ‚â´ ShortComplex.leftRightHomologyComparison'
    (h‚Çó.map F) (h·µ£.map F) ‚â´ (h·µ£.map F).Œπ
  ¬∑ rw [ShortComplex.œÄ_leftRightHomologyComparison'_Œπ]; simp
  ¬∑ simp

end ShortComplex

namespace Functor

variable (F : C ‚•§ D) [PreservesZeroMorphisms F] (S : ShortComplex C) {S‚ÇÅ S‚ÇÇ : ShortComplex C}

/-- A functor preserves the left homology of a short complex `S` if it preserves all the
left homology data of `S`. -/
class PreservesLeftHomologyOf : Prop where
  /-- the functor preserves all the left homology data of the short complex -/
  isPreservedBy : ‚àÄ (h : S.LeftHomologyData), h.IsPreservedBy F

/-- A functor preserves the right homology of a short complex `S` if it preserves all the
right homology data of `S`. -/
class PreservesRightHomologyOf : Prop where
  /-- the functor preserves all the right homology data of the short complex -/
  isPreservedBy : ‚àÄ (h : S.RightHomologyData), h.IsPreservedBy F

instance PreservesHomology.preservesLeftHomologyOf [F.PreservesHomology] :
    F.PreservesLeftHomologyOf S := ‚ü®inferInstance‚ü©

instance PreservesHomology.preservesRightHomologyOf [F.PreservesHomology] :
    F.PreservesRightHomologyOf S := ‚ü®inferInstance‚ü©

variable {S}

/-- If a functor preserves a certain left homology data of a short complex `S`, then it
preserves the left homology of `S`. -/
lemma PreservesLeftHomologyOf.mk' (h : S.LeftHomologyData) [h.IsPreservedBy F] :
    F.PreservesLeftHomologyOf S where
  isPreservedBy h' :=
    { g := ShortComplex.LeftHomologyData.IsPreservedBy.hg h F
      f' := by
        have := ShortComplex.LeftHomologyData.IsPreservedBy.hf' h F
        let e : parallelPair h.f' 0 ‚âÖ parallelPair h'.f' 0 :=
          parallelPair.ext (Iso.refl _) (ShortComplex.cyclesMapIso' (Iso.refl S) h h')
            (by simp) (by simp)
        exact preservesColimit_of_iso_diagram F e }

/-- If a functor preserves a certain right homology data of a short complex `S`, then it
preserves the right homology of `S`. -/
lemma PreservesRightHomologyOf.mk' (h : S.RightHomologyData) [h.IsPreservedBy F] :
    F.PreservesRightHomologyOf S where
  isPreservedBy h' :=
    { f := ShortComplex.RightHomologyData.IsPreservedBy.hf h F
      g' := by
        have := ShortComplex.RightHomologyData.IsPreservedBy.hg' h F
        let e : parallelPair h.g' 0 ‚âÖ parallelPair h'.g' 0 :=
          parallelPair.ext (ShortComplex.opcyclesMapIso' (Iso.refl S) h h') (Iso.refl _)
            (by simp) (by simp)
        exact preservesLimit_of_iso_diagram F e }

end Functor

namespace ShortComplex

variable {S : ShortComplex C} (h‚ÇÅ : S.LeftHomologyData) (h‚ÇÇ : S.RightHomologyData)
  (F : C ‚•§ D) [F.PreservesZeroMorphisms]

instance LeftHomologyData.isPreservedBy_of_preserves [F.PreservesLeftHomologyOf S] :
    h‚ÇÅ.IsPreservedBy F :=
  Functor.PreservesLeftHomologyOf.isPreservedBy _

instance RightHomologyData.isPreservedBy_of_preserves [F.PreservesRightHomologyOf S] :
    h‚ÇÇ.IsPreservedBy F :=
  Functor.PreservesRightHomologyOf.isPreservedBy _

variable (S)

instance hasLeftHomology_of_preserves [S.HasLeftHomology] [F.PreservesLeftHomologyOf S] :
    (S.map F).HasLeftHomology :=
  HasLeftHomology.mk' (S.leftHomologyData.map F)

instance hasLeftHomology_of_preserves' [S.HasLeftHomology] [F.PreservesLeftHomologyOf S] :
    (F.mapShortComplex.obj S).HasLeftHomology := by
  dsimp; infer_instance

instance hasRightHomology_of_preserves [S.HasRightHomology] [F.PreservesRightHomologyOf S] :
    (S.map F).HasRightHomology :=
  HasRightHomology.mk' (S.rightHomologyData.map F)

instance hasRightHomology_of_preserves' [S.HasRightHomology] [F.PreservesRightHomologyOf S] :
    (F.mapShortComplex.obj S).HasRightHomology := by
  dsimp; infer_instance

instance hasHomology_of_preserves [S.HasHomology] [F.PreservesLeftHomologyOf S]
    [F.PreservesRightHomologyOf S] :
    (S.map F).HasHomology :=
  HasHomology.mk' (S.homologyData.map F)

instance hasHomology_of_preserves' [S.HasHomology] [F.PreservesLeftHomologyOf S]
    [F.PreservesRightHomologyOf S] :
    (F.mapShortComplex.obj S).HasHomology := by
  dsimp; infer_instance

section

variable
  (hl : S.LeftHomologyData) (hr : S.RightHomologyData)
  {S‚ÇÅ S‚ÇÇ : ShortComplex C} (œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ)
  (hl‚ÇÅ : S‚ÇÅ.LeftHomologyData) (hr‚ÇÅ : S‚ÇÅ.RightHomologyData)
  (hl‚ÇÇ : S‚ÇÇ.LeftHomologyData) (hr‚ÇÇ : S‚ÇÇ.RightHomologyData)
  (h‚ÇÅ : S‚ÇÅ.HomologyData) (h‚ÇÇ : S‚ÇÇ.HomologyData)
  (F : C ‚•§ D) [F.PreservesZeroMorphisms]

namespace LeftHomologyData

variable [hl‚ÇÅ.IsPreservedBy F] [hl‚ÇÇ.IsPreservedBy F]

lemma map_cyclesMap' : F.map (ShortComplex.cyclesMap' œÜ hl‚ÇÅ hl‚ÇÇ) =
    ShortComplex.cyclesMap' (F.mapShortComplex.map œÜ) (hl‚ÇÅ.map F) (hl‚ÇÇ.map F) := by
  have Œ≥ : ShortComplex.LeftHomologyMapData œÜ hl‚ÇÅ hl‚ÇÇ := default
  rw [Œ≥.cyclesMap'_eq, (Œ≥.map F).cyclesMap'_eq, ShortComplex.LeftHomologyMapData.map_œÜK]

lemma map_leftHomologyMap' : F.map (ShortComplex.leftHomologyMap' œÜ hl‚ÇÅ hl‚ÇÇ) =
    ShortComplex.leftHomologyMap' (F.mapShortComplex.map œÜ) (hl‚ÇÅ.map F) (hl‚ÇÇ.map F) := by
  have Œ≥ : ShortComplex.LeftHomologyMapData œÜ hl‚ÇÅ hl‚ÇÇ := default
  rw [Œ≥.leftHomologyMap'_eq, (Œ≥.map F).leftHomologyMap'_eq,
    ShortComplex.LeftHomologyMapData.map_œÜH]

end LeftHomologyData

namespace RightHomologyData

variable [hr‚ÇÅ.IsPreservedBy F] [hr‚ÇÇ.IsPreservedBy F]

lemma map_opcyclesMap' : F.map (ShortComplex.opcyclesMap' œÜ hr‚ÇÅ hr‚ÇÇ) =
    ShortComplex.opcyclesMap' (F.mapShortComplex.map œÜ) (hr‚ÇÅ.map F) (hr‚ÇÇ.map F) := by
  have Œ≥ : ShortComplex.RightHomologyMapData œÜ hr‚ÇÅ hr‚ÇÇ := default
  rw [Œ≥.opcyclesMap'_eq, (Œ≥.map F).opcyclesMap'_eq, ShortComplex.RightHomologyMapData.map_œÜQ]

lemma map_rightHomologyMap' : F.map (ShortComplex.rightHomologyMap' œÜ hr‚ÇÅ hr‚ÇÇ) =
    ShortComplex.rightHomologyMap' (F.mapShortComplex.map œÜ) (hr‚ÇÅ.map F) (hr‚ÇÇ.map F) := by
  have Œ≥ : ShortComplex.RightHomologyMapData œÜ hr‚ÇÅ hr‚ÇÇ := default
  rw [Œ≥.rightHomologyMap'_eq, (Œ≥.map F).rightHomologyMap'_eq,
    ShortComplex.RightHomologyMapData.map_œÜH]

end RightHomologyData

lemma HomologyData.map_homologyMap'
    [h‚ÇÅ.left.IsPreservedBy F] [h‚ÇÅ.right.IsPreservedBy F]
    [h‚ÇÇ.left.IsPreservedBy F] [h‚ÇÇ.right.IsPreservedBy F] :
    F.map (ShortComplex.homologyMap' œÜ h‚ÇÅ h‚ÇÇ) =
      ShortComplex.homologyMap' (F.mapShortComplex.map œÜ) (h‚ÇÅ.map F) (h‚ÇÇ.map F) :=
  LeftHomologyData.map_leftHomologyMap' _ _ _ _

/-- When a functor `F` preserves the left homology of a short complex `S`, this is the
canonical isomorphism `(S.map F).cycles ‚âÖ F.obj S.cycles`. -/
noncomputable def mapCyclesIso [S.HasLeftHomology] [F.PreservesLeftHomologyOf S] :
    (S.map F).cycles ‚âÖ F.obj S.cycles :=
  (S.leftHomologyData.map F).cyclesIso

@[reassoc (attr := simp)]
lemma mapCyclesIso_hom_iCycles [S.HasLeftHomology] [F.PreservesLeftHomologyOf S] :
    (S.mapCyclesIso F).hom ‚â´ F.map S.iCycles = (S.map F).iCycles := by
  apply LeftHomologyData.cyclesIso_hom_comp_i

/-- When a functor `F` preserves the left homology of a short complex `S`, this is the
canonical isomorphism `(S.map F).leftHomology ‚âÖ F.obj S.leftHomology`. -/
noncomputable def mapLeftHomologyIso [S.HasLeftHomology] [F.PreservesLeftHomologyOf S] :
    (S.map F).leftHomology ‚âÖ F.obj S.leftHomology :=
  (S.leftHomologyData.map F).leftHomologyIso

/-- When a functor `F` preserves the right homology of a short complex `S`, this is the
canonical isomorphism `(S.map F).opcycles ‚âÖ F.obj S.opcycles`. -/
noncomputable def mapOpcyclesIso [S.HasRightHomology] [F.PreservesRightHomologyOf S] :
    (S.map F).opcycles ‚âÖ F.obj S.opcycles :=
  (S.rightHomologyData.map F).opcyclesIso

/-- When a functor `F` preserves the right homology of a short complex `S`, this is the
canonical isomorphism `(S.map F).rightHomology ‚âÖ F.obj S.rightHomology`. -/
noncomputable def mapRightHomologyIso [S.HasRightHomology] [F.PreservesRightHomologyOf S] :
    (S.map F).rightHomology ‚âÖ F.obj S.rightHomology :=
  (S.rightHomologyData.map F).rightHomologyIso

/-- When a functor `F` preserves the left homology of a short complex `S`, this is the
canonical isomorphism `(S.map F).homology ‚âÖ F.obj S.homology`. -/
noncomputable def mapHomologyIso [S.HasHomology] [(S.map F).HasHomology]
    [F.PreservesLeftHomologyOf S] :
    (S.map F).homology ‚âÖ F.obj S.homology :=
  (S.homologyData.left.map F).homologyIso

/-- When a functor `F` preserves the right homology of a short complex `S`, this is the
canonical isomorphism `(S.map F).homology ‚âÖ F.obj S.homology`. -/
noncomputable def mapHomologyIso' [S.HasHomology] [(S.map F).HasHomology]
    [F.PreservesRightHomologyOf S] :
    (S.map F).homology ‚âÖ F.obj S.homology :=
  (S.homologyData.right.map F).homologyIso ‚â™‚â´ F.mapIso S.homologyData.right.homologyIso.symm

variable {S}

lemma LeftHomologyData.mapCyclesIso_eq [S.HasLeftHomology]
    [F.PreservesLeftHomologyOf S] :
    S.mapCyclesIso F = (hl.map F).cyclesIso ‚â™‚â´ F.mapIso hl.cyclesIso.symm := by
  ext
  dsimp [mapCyclesIso, cyclesIso]
  simp only [map_cyclesMap', ‚Üê cyclesMap'_comp, Functor.map_id, comp_id,
    Functor.mapShortComplex_obj]

lemma LeftHomologyData.mapLeftHomologyIso_eq [S.HasLeftHomology]
    [F.PreservesLeftHomologyOf S] :
    S.mapLeftHomologyIso F = (hl.map F).leftHomologyIso ‚â™‚â´ F.mapIso hl.leftHomologyIso.symm := by
  ext
  dsimp [mapLeftHomologyIso, leftHomologyIso]
  simp only [map_leftHomologyMap', ‚Üê leftHomologyMap'_comp, Functor.map_id, comp_id,
    Functor.mapShortComplex_obj]

lemma RightHomologyData.mapOpcyclesIso_eq [S.HasRightHomology]
    [F.PreservesRightHomologyOf S] :
    S.mapOpcyclesIso F = (hr.map F).opcyclesIso ‚â™‚â´ F.mapIso hr.opcyclesIso.symm := by
  ext
  dsimp [mapOpcyclesIso, opcyclesIso]
  simp only [map_opcyclesMap', ‚Üê opcyclesMap'_comp, Functor.map_id, comp_id,
    Functor.mapShortComplex_obj]

lemma RightHomologyData.mapRightHomologyIso_eq [S.HasRightHomology]
    [F.PreservesRightHomologyOf S] :
    S.mapRightHomologyIso F = (hr.map F).rightHomologyIso ‚â™‚â´
      F.mapIso hr.rightHomologyIso.symm := by
  ext
  dsimp [mapRightHomologyIso, rightHomologyIso]
  simp only [map_rightHomologyMap', ‚Üê rightHomologyMap'_comp, Functor.map_id, comp_id,
    Functor.mapShortComplex_obj]

lemma LeftHomologyData.mapHomologyIso_eq [S.HasHomology]
    [(S.map F).HasHomology] [F.PreservesLeftHomologyOf S] :
    S.mapHomologyIso F = (hl.map F).homologyIso ‚â™‚â´ F.mapIso hl.homologyIso.symm := by
  ext
  dsimp only [mapHomologyIso, homologyIso, ShortComplex.leftHomologyIso,
    leftHomologyMapIso', leftHomologyIso, Functor.mapIso,
    Iso.symm, Iso.trans, Iso.refl]
  simp only [map_leftHomologyMap', ‚Üê leftHomologyMap'_comp, comp_id, Functor.map_id,
    Functor.mapShortComplex_obj]

lemma RightHomologyData.mapHomologyIso'_eq [S.HasHomology]
    [(S.map F).HasHomology] [F.PreservesRightHomologyOf S] :
    S.mapHomologyIso' F = (hr.map F).homologyIso ‚â™‚â´ F.mapIso hr.homologyIso.symm := by
  ext
  dsimp only [Iso.trans, Iso.symm, Iso.refl, Functor.mapIso, mapHomologyIso', homologyIso,
    rightHomologyIso, rightHomologyMapIso', ShortComplex.rightHomologyIso]
  simp only [assoc, F.map_comp, map_rightHomologyMap', ‚Üê rightHomologyMap'_comp_assoc]

@[reassoc]
lemma mapCyclesIso_hom_naturality [S‚ÇÅ.HasLeftHomology] [S‚ÇÇ.HasLeftHomology]
    [F.PreservesLeftHomologyOf S‚ÇÅ] [F.PreservesLeftHomologyOf S‚ÇÇ] :
    cyclesMap (F.mapShortComplex.map œÜ) ‚â´ (S‚ÇÇ.mapCyclesIso F).hom =
      (S‚ÇÅ.mapCyclesIso F).hom ‚â´ F.map (cyclesMap œÜ) := by
  dsimp only [cyclesMap, mapCyclesIso, LeftHomologyData.cyclesIso, cyclesMapIso', Iso.refl]
  simp only [LeftHomologyData.map_cyclesMap', Functor.mapShortComplex_obj, ‚Üê cyclesMap'_comp,
    comp_id, id_comp]

@[reassoc]
lemma mapCyclesIso_inv_naturality [S‚ÇÅ.HasLeftHomology] [S‚ÇÇ.HasLeftHomology]
    [F.PreservesLeftHomologyOf S‚ÇÅ] [F.PreservesLeftHomologyOf S‚ÇÇ] :
    F.map (cyclesMap œÜ) ‚â´ (S‚ÇÇ.mapCyclesIso F).inv =
      (S‚ÇÅ.mapCyclesIso F).inv ‚â´ cyclesMap (F.mapShortComplex.map œÜ) := by
  rw [‚Üê cancel_epi (S‚ÇÅ.mapCyclesIso F).hom, ‚Üê mapCyclesIso_hom_naturality_assoc,
    Iso.hom_inv_id, comp_id, Iso.hom_inv_id_assoc]

@[reassoc]
lemma mapLeftHomologyIso_hom_naturality [S‚ÇÅ.HasLeftHomology] [S‚ÇÇ.HasLeftHomology]
    [F.PreservesLeftHomologyOf S‚ÇÅ] [F.PreservesLeftHomologyOf S‚ÇÇ] :
    leftHomologyMap (F.mapShortComplex.map œÜ) ‚â´ (S‚ÇÇ.mapLeftHomologyIso F).hom =
      (S‚ÇÅ.mapLeftHomologyIso F).hom ‚â´ F.map (leftHomologyMap œÜ) := by
  dsimp only [leftHomologyMap, mapLeftHomologyIso, LeftHomologyData.leftHomologyIso,
    leftHomologyMapIso', Iso.refl]
  simp only [LeftHomologyData.map_leftHomologyMap', Functor.mapShortComplex_obj,
    ‚Üê leftHomologyMap'_comp, comp_id, id_comp]

@[reassoc]
lemma mapLeftHomologyIso_inv_naturality [S‚ÇÅ.HasLeftHomology] [S‚ÇÇ.HasLeftHomology]
    [F.PreservesLeftHomologyOf S‚ÇÅ] [F.PreservesLeftHomologyOf S‚ÇÇ] :
    F.map (leftHomologyMap œÜ) ‚â´ (S‚ÇÇ.mapLeftHomologyIso F).inv =
      (S‚ÇÅ.mapLeftHomologyIso F).inv ‚â´ leftHomologyMap (F.mapShortComplex.map œÜ) := by
  rw [‚Üê cancel_epi (S‚ÇÅ.mapLeftHomologyIso F).hom, ‚Üê mapLeftHomologyIso_hom_naturality_assoc,
    Iso.hom_inv_id, comp_id, Iso.hom_inv_id_assoc]

@[reassoc]
lemma mapOpcyclesIso_hom_naturality [S‚ÇÅ.HasRightHomology] [S‚ÇÇ.HasRightHomology]
    [F.PreservesRightHomologyOf S‚ÇÅ] [F.PreservesRightHomologyOf S‚ÇÇ] :
    opcyclesMap (F.mapShortComplex.map œÜ) ‚â´ (S‚ÇÇ.mapOpcyclesIso F).hom =
      (S‚ÇÅ.mapOpcyclesIso F).hom ‚â´ F.map (opcyclesMap œÜ) := by
  dsimp only [opcyclesMap, mapOpcyclesIso, RightHomologyData.opcyclesIso,
    opcyclesMapIso', Iso.refl]
  simp only [RightHomologyData.map_opcyclesMap', Functor.mapShortComplex_obj, ‚Üê opcyclesMap'_comp,
    comp_id, id_comp]

@[reassoc]
lemma mapOpcyclesIso_inv_naturality [S‚ÇÅ.HasRightHomology] [S‚ÇÇ.HasRightHomology]
    [F.PreservesRightHomologyOf S‚ÇÅ] [F.PreservesRightHomologyOf S‚ÇÇ] :
    F.map (opcyclesMap œÜ) ‚â´ (S‚ÇÇ.mapOpcyclesIso F).inv =
      (S‚ÇÅ.mapOpcyclesIso F).inv ‚â´ opcyclesMap (F.mapShortComplex.map œÜ) := by
  rw [‚Üê cancel_epi (S‚ÇÅ.mapOpcyclesIso F).hom, ‚Üê mapOpcyclesIso_hom_naturality_assoc,
    Iso.hom_inv_id, comp_id, Iso.hom_inv_id_assoc]

@[reassoc]
lemma mapRightHomologyIso_hom_naturality [S‚ÇÅ.HasRightHomology] [S‚ÇÇ.HasRightHomology]
    [F.PreservesRightHomologyOf S‚ÇÅ] [F.PreservesRightHomologyOf S‚ÇÇ] :
    rightHomologyMap (F.mapShortComplex.map œÜ) ‚â´ (S‚ÇÇ.mapRightHomologyIso F).hom =
      (S‚ÇÅ.mapRightHomologyIso F).hom ‚â´ F.map (rightHomologyMap œÜ) := by
  dsimp only [rightHomologyMap, mapRightHomologyIso, RightHomologyData.rightHomologyIso,
    rightHomologyMapIso', Iso.refl]
  simp only [RightHomologyData.map_rightHomologyMap', Functor.mapShortComplex_obj,
    ‚Üê rightHomologyMap'_comp, comp_id, id_comp]

@[reassoc]
lemma mapRightHomologyIso_inv_naturality [S‚ÇÅ.HasRightHomology] [S‚ÇÇ.HasRightHomology]
    [F.PreservesRightHomologyOf S‚ÇÅ] [F.PreservesRightHomologyOf S‚ÇÇ] :
    F.map (rightHomologyMap œÜ) ‚â´ (S‚ÇÇ.mapRightHomologyIso F).inv =
      (S‚ÇÅ.mapRightHomologyIso F).inv ‚â´ rightHomologyMap (F.mapShortComplex.map œÜ) := by
  rw [‚Üê cancel_epi (S‚ÇÅ.mapRightHomologyIso F).hom, ‚Üê mapRightHomologyIso_hom_naturality_assoc,
    Iso.hom_inv_id, comp_id, Iso.hom_inv_id_assoc]

@[reassoc]
lemma mapHomologyIso_hom_naturality [S‚ÇÅ.HasHomology] [S‚ÇÇ.HasHomology]
    [(S‚ÇÅ.map F).HasHomology] [(S‚ÇÇ.map F).HasHomology]
    [F.PreservesLeftHomologyOf S‚ÇÅ] [F.PreservesLeftHomologyOf S‚ÇÇ] :
    @homologyMap _ _ _ (S‚ÇÅ.map F) (S‚ÇÇ.map F) (F.mapShortComplex.map œÜ) _ _ ‚â´
      (S‚ÇÇ.mapHomologyIso F).hom = (S‚ÇÅ.mapHomologyIso F).hom ‚â´ F.map (homologyMap œÜ) := by
  dsimp only [homologyMap, homologyMap', mapHomologyIso, LeftHomologyData.homologyIso,
    LeftHomologyData.leftHomologyIso, leftHomologyMapIso', leftHomologyIso,
    Iso.symm, Iso.trans, Iso.refl]
  simp only [LeftHomologyData.map_leftHomologyMap', ‚Üê leftHomologyMap'_comp, comp_id, id_comp]

@[reassoc]
lemma mapHomologyIso_inv_naturality [S‚ÇÅ.HasHomology] [S‚ÇÇ.HasHomology]
    [(S‚ÇÅ.map F).HasHomology] [(S‚ÇÇ.map F).HasHomology]
    [F.PreservesLeftHomologyOf S‚ÇÅ] [F.PreservesLeftHomologyOf S‚ÇÇ] :
    F.map (homologyMap œÜ) ‚â´ (S‚ÇÇ.mapHomologyIso F).inv =
      (S‚ÇÅ.mapHomologyIso F).inv ‚â´
      @homologyMap _ _ _ (S‚ÇÅ.map F) (S‚ÇÇ.map F) (F.mapShortComplex.map œÜ) _ _ := by
  rw [‚Üê cancel_epi (S‚ÇÅ.mapHomologyIso F).hom, ‚Üê mapHomologyIso_hom_naturality_assoc,
    Iso.hom_inv_id, comp_id, Iso.hom_inv_id_assoc]

@[reassoc]
lemma mapHomologyIso'_hom_naturality [S‚ÇÅ.HasHomology] [S‚ÇÇ.HasHomology]
    [(S‚ÇÅ.map F).HasHomology] [(S‚ÇÇ.map F).HasHomology]
    [F.PreservesRightHomologyOf S‚ÇÅ] [F.PreservesRightHomologyOf S‚ÇÇ] :
    @homologyMap _ _ _ (S‚ÇÅ.map F) (S‚ÇÇ.map F) (F.mapShortComplex.map œÜ) _ _ ‚â´
      (S‚ÇÇ.mapHomologyIso' F).hom = (S‚ÇÅ.mapHomologyIso' F).hom ‚â´ F.map (homologyMap œÜ) := by
  dsimp only [Iso.trans, Iso.symm, Functor.mapIso, mapHomologyIso']
  simp only [‚Üê RightHomologyData.rightHomologyIso_hom_naturality_assoc _
    ((homologyData S‚ÇÅ).right.map F) ((homologyData S‚ÇÇ).right.map F), assoc,
    ‚Üê RightHomologyData.map_rightHomologyMap', ‚Üê F.map_comp,
    RightHomologyData.rightHomologyIso_inv_naturality _
      (homologyData S‚ÇÅ).right (homologyData S‚ÇÇ).right]

@[reassoc]
lemma mapHomologyIso'_inv_naturality [S‚ÇÅ.HasHomology] [S‚ÇÇ.HasHomology]
    [(S‚ÇÅ.map F).HasHomology] [(S‚ÇÇ.map F).HasHomology]
    [F.PreservesRightHomologyOf S‚ÇÅ] [F.PreservesRightHomologyOf S‚ÇÇ] :
    F.map (homologyMap œÜ) ‚â´ (S‚ÇÇ.mapHomologyIso' F).inv = (S‚ÇÅ.mapHomologyIso' F).inv ‚â´
      @homologyMap _ _ _ (S‚ÇÅ.map F) (S‚ÇÇ.map F) (F.mapShortComplex.map œÜ) _ _ := by
  rw [‚Üê cancel_epi (S‚ÇÅ.mapHomologyIso' F).hom, ‚Üê mapHomologyIso'_hom_naturality_assoc,
    Iso.hom_inv_id, comp_id, Iso.hom_inv_id_assoc]

variable (S)

lemma mapHomologyIso'_eq_mapHomologyIso [S.HasHomology] [F.PreservesLeftHomologyOf S]
    [F.PreservesRightHomologyOf S] :
    S.mapHomologyIso' F = S.mapHomologyIso F := by
  ext
  rw [S.homologyData.left.mapHomologyIso_eq F, S.homologyData.right.mapHomologyIso'_eq F]
  dsimp only [Iso.trans, Iso.symm, Iso.refl, Functor.mapIso, RightHomologyData.homologyIso,
    rightHomologyIso, RightHomologyData.rightHomologyIso, LeftHomologyData.homologyIso,
    leftHomologyIso, LeftHomologyData.leftHomologyIso]
  simp only [RightHomologyData.map_H, rightHomologyMapIso'_inv, rightHomologyMapIso'_hom, assoc,
    Functor.map_comp, RightHomologyData.map_rightHomologyMap', Functor.mapShortComplex_obj,
    Functor.map_id, LeftHomologyData.map_H, leftHomologyMapIso'_inv, leftHomologyMapIso'_hom,
    LeftHomologyData.map_leftHomologyMap', ‚Üê rightHomologyMap'_comp_assoc, ‚Üê leftHomologyMap'_comp,
    id_comp]
  have Œ≥ : HomologyMapData (ùüô (S.map F)) (map S F).homologyData (S.homologyData.map F) := default
  have eq := Œ≥.comm
  rw [‚Üê Œ≥.left.leftHomologyMap'_eq, ‚Üê Œ≥.right.rightHomologyMap'_eq] at eq
  dsimp at eq
  simp only [‚Üê reassoc_of% eq, ‚Üê F.map_comp, Iso.hom_inv_id, F.map_id, comp_id]

end

section

variable {S}
  {F G : C ‚•§ D} [F.PreservesZeroMorphisms] [G.PreservesZeroMorphisms]
  [F.PreservesLeftHomologyOf S] [G.PreservesLeftHomologyOf S]
  [F.PreservesRightHomologyOf S] [G.PreservesRightHomologyOf S]

/-- Given a natural transformation `œÑ : F ‚ü∂ G` between functors `C ‚•§ D` which preserve
the left homology of a short complex `S`, and a left homology data for `S`,
this is the left homology map data for the morphism `S.mapNatTrans œÑ`
obtained by evaluating `œÑ`. -/
@[simps]
noncomputable def LeftHomologyMapData.natTransApp (h : LeftHomologyData S) (œÑ : F ‚ü∂ G) :
    LeftHomologyMapData (S.mapNatTrans œÑ) (h.map F) (h.map G) where
  œÜK := œÑ.app h.K
  œÜH := œÑ.app h.H

/-- Given a natural transformation `œÑ : F ‚ü∂ G` between functors `C ‚•§ D` which preserve
the right homology of a short complex `S`, and a right homology data for `S`,
this is the right homology map data for the morphism `S.mapNatTrans œÑ`
obtained by evaluating `œÑ`. -/
@[simps]
noncomputable def RightHomologyMapData.natTransApp (h : RightHomologyData S) (œÑ : F ‚ü∂ G) :
    RightHomologyMapData (S.mapNatTrans œÑ) (h.map F) (h.map G) where
  œÜQ := œÑ.app h.Q
  œÜH := œÑ.app h.H

/-- Given a natural transformation `œÑ : F ‚ü∂ G` between functors `C ‚•§ D` which preserve
the homology of a short complex `S`, and a homology data for `S`,
this is the homology map data for the morphism `S.mapNatTrans œÑ`
obtained by evaluating `œÑ`. -/
@[simps]
noncomputable def HomologyMapData.natTransApp (h : HomologyData S) (œÑ : F ‚ü∂ G) :
    HomologyMapData (S.mapNatTrans œÑ) (h.map F) (h.map G) where
  left := LeftHomologyMapData.natTransApp h.left œÑ
  right := RightHomologyMapData.natTransApp h.right œÑ

variable (S)

lemma homologyMap_mapNatTrans [S.HasHomology] (œÑ : F ‚ü∂ G) :
    homologyMap (S.mapNatTrans œÑ) =
      (S.mapHomologyIso F).hom ‚â´ œÑ.app S.homology ‚â´ (S.mapHomologyIso G).inv :=
  (LeftHomologyMapData.natTransApp S.homologyData.left œÑ).homologyMap_eq

end

section

variable [HasKernels C] [HasCokernels C] [HasKernels D] [HasCokernels D]

/-- The natural isomorphism
`F.mapShortComplex ‚ãô cyclesFunctor D ‚âÖ cyclesFunctor C ‚ãô F`
for a functor `F : C ‚•§ D` which preserves homology. -/
noncomputable def cyclesFunctorIso [F.PreservesHomology] :
    F.mapShortComplex ‚ãô ShortComplex.cyclesFunctor D ‚âÖ
      ShortComplex.cyclesFunctor C ‚ãô F :=
  NatIso.ofComponents (fun S ‚Ü¶ S.mapCyclesIso F)
    (fun f ‚Ü¶ ShortComplex.mapCyclesIso_hom_naturality f F)

/-- The natural isomorphism
`F.mapShortComplex ‚ãô leftHomologyFunctor D ‚âÖ leftHomologyFunctor C ‚ãô F`
for a functor `F : C ‚•§ D` which preserves homology. -/
noncomputable def leftHomologyFunctorIso [F.PreservesHomology] :
    F.mapShortComplex ‚ãô ShortComplex.leftHomologyFunctor D ‚âÖ
      ShortComplex.leftHomologyFunctor C ‚ãô F :=
  NatIso.ofComponents (fun S ‚Ü¶ S.mapLeftHomologyIso F)
    (fun f ‚Ü¶ ShortComplex.mapLeftHomologyIso_hom_naturality f F)

/-- The natural isomorphism
`F.mapShortComplex ‚ãô opcyclesFunctor D ‚âÖ opcyclesFunctor C ‚ãô F`
for a functor `F : C ‚•§ D` which preserves homology. -/
noncomputable def opcyclesFunctorIso [F.PreservesHomology] :
    F.mapShortComplex ‚ãô ShortComplex.opcyclesFunctor D ‚âÖ
      ShortComplex.opcyclesFunctor C ‚ãô F :=
  NatIso.ofComponents (fun S ‚Ü¶ S.mapOpcyclesIso F)
    (fun f ‚Ü¶ ShortComplex.mapOpcyclesIso_hom_naturality f F)

/-- The natural isomorphism
`F.mapShortComplex ‚ãô rightHomologyFunctor D ‚âÖ rightHomologyFunctor C ‚ãô F`
for a functor `F : C ‚•§ D` which preserves homology. -/
noncomputable def rightHomologyFunctorIso [F.PreservesHomology] :
    F.mapShortComplex ‚ãô ShortComplex.rightHomologyFunctor D ‚âÖ
      ShortComplex.rightHomologyFunctor C ‚ãô F :=
  NatIso.ofComponents (fun S ‚Ü¶ S.mapRightHomologyIso F)
    (fun f ‚Ü¶ ShortComplex.mapRightHomologyIso_hom_naturality f F)

end

/-- The natural isomorphism
`F.mapShortComplex ‚ãô homologyFunctor D ‚âÖ homologyFunctor C ‚ãô F`
for a functor `F : C ‚•§ D` which preserves homology. -/
noncomputable def homologyFunctorIso
    [CategoryWithHomology C] [CategoryWithHomology D] [F.PreservesHomology] :
    F.mapShortComplex ‚ãô ShortComplex.homologyFunctor D ‚âÖ
      ShortComplex.homologyFunctor C ‚ãô F :=
  NatIso.ofComponents (fun S ‚Ü¶ S.mapHomologyIso F)
    (fun f ‚Ü¶ ShortComplex.mapHomologyIso_hom_naturality f F)

section

variable
  {S‚ÇÅ S‚ÇÇ : ShortComplex C} {œÜ : S‚ÇÅ ‚ü∂ S‚ÇÇ}
  {hl‚ÇÅ : S‚ÇÅ.LeftHomologyData} {hr‚ÇÅ : S‚ÇÅ.RightHomologyData}
  {hl‚ÇÇ : S‚ÇÇ.LeftHomologyData} {hr‚ÇÇ : S‚ÇÇ.RightHomologyData}
  (œàl : LeftHomologyMapData œÜ hl‚ÇÅ hl‚ÇÇ)
  (œàr : RightHomologyMapData œÜ hr‚ÇÅ hr‚ÇÇ)

lemma LeftHomologyMapData.quasiIso_map_iff
    [(F.mapShortComplex.obj S‚ÇÅ).HasHomology]
    [(F.mapShortComplex.obj S‚ÇÇ).HasHomology]
    [hl‚ÇÅ.IsPreservedBy F] [hl‚ÇÇ.IsPreservedBy F] :
    QuasiIso (F.mapShortComplex.map œÜ) ‚Üî IsIso (F.map œàl.œÜH) :=
  (œàl.map F).quasiIso_iff

lemma RightHomologyMapData.quasiIso_map_iff
    [(F.mapShortComplex.obj S‚ÇÅ).HasHomology]
    [(F.mapShortComplex.obj S‚ÇÇ).HasHomology]
    [hr‚ÇÅ.IsPreservedBy F] [hr‚ÇÇ.IsPreservedBy F] :
    QuasiIso (F.mapShortComplex.map œÜ) ‚Üî IsIso (F.map œàr.œÜH) :=
  (œàr.map F).quasiIso_iff

variable (œÜ) [S‚ÇÅ.HasHomology] [S‚ÇÇ.HasHomology]
    [(F.mapShortComplex.obj S‚ÇÅ).HasHomology] [(F.mapShortComplex.obj S‚ÇÇ).HasHomology]

instance quasiIso_map_of_preservesLeftHomology
    [F.PreservesLeftHomologyOf S‚ÇÅ] [F.PreservesLeftHomologyOf S‚ÇÇ]
    [QuasiIso œÜ] : QuasiIso (F.mapShortComplex.map œÜ) := by
  have Œ≥ : LeftHomologyMapData œÜ S‚ÇÅ.leftHomologyData S‚ÇÇ.leftHomologyData := default
  have : IsIso Œ≥.œÜH := by
    rw [‚Üê Œ≥.quasiIso_iff]
    infer_instance
  rw [(Œ≥.map F).quasiIso_iff, LeftHomologyMapData.map_œÜH]
  infer_instance

lemma quasiIso_map_iff_of_preservesLeftHomology
    [F.PreservesLeftHomologyOf S‚ÇÅ] [F.PreservesLeftHomologyOf S‚ÇÇ]
    [F.ReflectsIsomorphisms] :
    QuasiIso (F.mapShortComplex.map œÜ) ‚Üî QuasiIso œÜ := by
  have Œ≥ : LeftHomologyMapData œÜ S‚ÇÅ.leftHomologyData S‚ÇÇ.leftHomologyData := default
  rw [Œ≥.quasiIso_iff, (Œ≥.map F).quasiIso_iff, LeftHomologyMapData.map_œÜH]
  constructor
  ¬∑ intro
    exact isIso_of_reflects_iso _ F
  ¬∑ intro
    infer_instance

instance quasiIso_map_of_preservesRightHomology
    [F.PreservesRightHomologyOf S‚ÇÅ] [F.PreservesRightHomologyOf S‚ÇÇ]
    [QuasiIso œÜ] : QuasiIso (F.mapShortComplex.map œÜ) := by
  have Œ≥ : RightHomologyMapData œÜ S‚ÇÅ.rightHomologyData S‚ÇÇ.rightHomologyData := default
  have : IsIso Œ≥.œÜH := by
    rw [‚Üê Œ≥.quasiIso_iff]
    infer_instance
  rw [(Œ≥.map F).quasiIso_iff, RightHomologyMapData.map_œÜH]
  infer_instance

lemma quasiIso_map_iff_of_preservesRightHomology
    [F.PreservesRightHomologyOf S‚ÇÅ] [F.PreservesRightHomologyOf S‚ÇÇ]
    [F.ReflectsIsomorphisms] :
    QuasiIso (F.mapShortComplex.map œÜ) ‚Üî QuasiIso œÜ := by
  have Œ≥ : RightHomologyMapData œÜ S‚ÇÅ.rightHomologyData S‚ÇÇ.rightHomologyData := default
  rw [Œ≥.quasiIso_iff, (Œ≥.map F).quasiIso_iff, RightHomologyMapData.map_œÜH]
  constructor
  ¬∑ intro
    exact isIso_of_reflects_iso _ F
  ¬∑ intro
    infer_instance

end

end ShortComplex

namespace Functor

variable (F : C ‚•§ D) [F.PreservesZeroMorphisms] (S : ShortComplex C)

/-- If a short complex `S` is such that `S.f = 0` and that the kernel of `S.g` is preserved
by a functor `F`, then `F` preserves the left homology of `S`. -/
lemma preservesLeftHomology_of_zero_f (hf : S.f = 0)
    [PreservesLimit (parallelPair S.g 0) F] :
    F.PreservesLeftHomologyOf S := ‚ü®fun h ‚Ü¶
  { g := by infer_instance
    f' := Limits.preservesCokernel_zero' _ _
      (by rw [‚Üê cancel_mono h.i, h.f'_i, zero_comp, hf]) }‚ü©

/-- If a short complex `S` is such that `S.g = 0` and that the cokernel of `S.f` is preserved
by a functor `F`, then `F` preserves the right homology of `S`. -/
lemma preservesRightHomology_of_zero_g (hg : S.g = 0)
    [PreservesColimit (parallelPair S.f 0) F] :
    F.PreservesRightHomologyOf S := ‚ü®fun h ‚Ü¶
  { f := by infer_instance
    g' := Limits.preservesKernel_zero' _ _
      (by rw [‚Üê cancel_epi h.p, h.p_g', comp_zero, hg]) }‚ü©

/-- If a short complex `S` is such that `S.g = 0` and that the cokernel of `S.f` is preserved
by a functor `F`, then `F` preserves the left homology of `S`. -/
lemma preservesLeftHomology_of_zero_g (hg : S.g = 0)
    [PreservesColimit (parallelPair S.f 0) F] :
    F.PreservesLeftHomologyOf S := ‚ü®fun h ‚Ü¶
  { g := by
      rw [hg]
      infer_instance
    f' := by
      have := h.isIso_i hg
      let e : parallelPair h.f' 0 ‚âÖ parallelPair S.f 0 :=
        parallelPair.ext (Iso.refl _) (asIso h.i) (by simp) (by simp)
      exact Limits.preservesColimit_of_iso_diagram F e.symm}‚ü©

/-- If a short complex `S` is such that `S.f = 0` and that the kernel of `S.g` is preserved
by a functor `F`, then `F` preserves the right homology of `S`. -/
lemma preservesRightHomology_of_zero_f (hf : S.f = 0)
    [PreservesLimit (parallelPair S.g 0) F] :
    F.PreservesRightHomologyOf S := ‚ü®fun h ‚Ü¶
  { f := by
      rw [hf]
      infer_instance
    g' := by
      have := h.isIso_p hf
      let e : parallelPair S.g 0 ‚âÖ parallelPair h.g' 0 :=
        parallelPair.ext (asIso h.p) (Iso.refl _) (by simp) (by simp)
      exact Limits.preservesLimit_of_iso_diagram F e }‚ü©

end Functor

lemma NatTrans.app_homology {F G : C ‚•§ D} (œÑ : F ‚ü∂ G)
    (S : ShortComplex C) [S.HasHomology] [F.PreservesZeroMorphisms] [G.PreservesZeroMorphisms]
    [F.PreservesLeftHomologyOf S] [G.PreservesLeftHomologyOf S] [F.PreservesRightHomologyOf S]
    [G.PreservesRightHomologyOf S] :
    œÑ.app S.homology = (S.mapHomologyIso F).inv ‚â´
      ShortComplex.homologyMap (S.mapNatTrans œÑ) ‚â´ (S.mapHomologyIso G).hom := by
  rw [ShortComplex.homologyMap_mapNatTrans, assoc, assoc, Iso.inv_hom_id,
    comp_id, Iso.inv_hom_id_assoc]

end CategoryTheory
