/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import Mathlib.Algebra.Homology.ShortComplex.LeftHomology
import Mathlib.CategoryTheory.Limits.Opposites

/-! RightHomology of short complexes

In this file, we define the dual notions to those defined in
`Algebra.Homology.ShortComplex.LeftHomology`. In particular, if `S : ShortComplex C` is
a short complex consisting of two composable maps `f : Xâ‚ âŸ¶ Xâ‚‚` and `g : Xâ‚‚ âŸ¶ Xâ‚ƒ` such
that `f â‰« g = 0`, we define `h : S.RightHomologyData` to be the datum of morphisms
`p : Xâ‚‚ âŸ¶ Q` and `Î¹ : H âŸ¶ Q` such that `Q` identifies to the cokernel of `f` and `H`
to the kernel of the induced map `g' : Q âŸ¶ Xâ‚ƒ`.

When such a `S.RightHomologyData` exists, we shall say that `[S.HasRightHomology]`
and (TODO) we define `S.rightHomology` to be the `H` field of a chosen right homology data.
Similarly, we define `S.opcycles` to be the `Q` field.

In `Homology.lean`, when `S` has two compatible left and right homology data
(i.e. they give the same `H` up to a canonical isomorphism), we shall define
`[S.HasHomology]` and `S.homology` (TODO).

-/

namespace CategoryTheory

open Category Limits

namespace ShortComplex

variable {C : Type _} [Category C] [HasZeroMorphisms C]
  (S : ShortComplex C) {Sâ‚ Sâ‚‚ Sâ‚ƒ : ShortComplex C}

/-- A right homology data for a short complex `S` consists of morphisms `p : S.Xâ‚‚ âŸ¶ Q` and
`Î¹ : H âŸ¶ Q` such that `p` identifies `Q` to the kernel of `f : S.Xâ‚ âŸ¶ S.Xâ‚‚`,
and that `Î¹` identifies `H` to the kernel of the induced map `g' : Q âŸ¶ S.Xâ‚ƒ` --/
structure RightHomologyData where
  /-- a choice of cokernel of `S.f : S.Xâ‚ âŸ¶ S.Xâ‚‚`-/
  Q : C
  /-- a choice of kernel of the induced morphism `S.g' : S.Q âŸ¶ Xâ‚ƒ`-/
  H : C
  /-- the projection from `S.Xâ‚‚` -/
  p : S.Xâ‚‚ âŸ¶ Q
  /-- the inclusion of the (right) homology in the chosen cokernel of `S.f` -/
  Î¹ : H âŸ¶ Q
  /-- the cokernel condition for `p` -/
  wp : S.f â‰« p = 0
  /-- `p : S.Xâ‚‚ âŸ¶ Q` is a cokernel of `S.f : S.Xâ‚ âŸ¶ S.Xâ‚‚` -/
  hp : IsColimit (CokernelCofork.ofÏ€ p wp)
  /-- the kernel condition for `Î¹` -/
  wÎ¹ : Î¹ â‰« hp.desc (CokernelCofork.ofÏ€ _ S.zero) = 0
  /-- `Î¹ : H âŸ¶ Q` is a kernel of `S.g' : Q âŸ¶ S.Xâ‚ƒ` -/
  hÎ¹ : IsLimit (KernelFork.ofÎ¹ Î¹ wÎ¹)

initialize_simps_projections RightHomologyData (-hp, -hÎ¹)

namespace RightHomologyData

/-- The chosen cokernels and kernels of the limits API give a `RightHomologyData` -/
@[simps]
noncomputable def ofHasCokernelOfHasKernel
    [HasCokernel S.f] [HasKernel (cokernel.desc S.f S.g S.zero)] :
    S.RightHomologyData :=
{ Q := cokernel S.f,
  H := kernel (cokernel.desc S.f S.g S.zero),
  p := cokernel.Ï€ _,
  Î¹ := kernel.Î¹ _,
  wp := cokernel.condition _,
  hp := cokernelIsCokernel _,
  wÎ¹ := kernel.condition _,
  hÎ¹ := kernelIsKernel _, }

attribute [reassoc (attr := simp)] wp wÎ¹

variable {S}
variable (h : S.RightHomologyData) {A : C}

instance : Epi h.p := âŸ¨fun _ _ => Cofork.IsColimit.hom_ext h.hpâŸ©

instance : Mono h.Î¹ := âŸ¨fun _ _ => Fork.IsLimit.hom_ext h.hÎ¹âŸ©

/-- Any morphism `k : S.Xâ‚‚ âŸ¶ A` such that `S.f â‰« k = 0` descends
to a morphism `Q âŸ¶ A` -/
def descQ (k : S.Xâ‚‚ âŸ¶ A) (hk : S.f â‰« k = 0) : h.Q âŸ¶ A :=
  h.hp.desc (CokernelCofork.ofÏ€ k hk)

@[reassoc (attr := simp)]
lemma p_descQ (k : S.Xâ‚‚ âŸ¶ A) (hk : S.f â‰« k = 0) :
  h.p â‰« h.descQ k hk = k :=
  h.hp.fac _ WalkingParallelPair.one

/-- The morphism from the (right) homology attached to a morphism
`k : S.Xâ‚‚ âŸ¶ A` such that `S.f â‰« k = 0`. -/
@[simp]
def descH (k : S.Xâ‚‚ âŸ¶ A) (hk : S.f â‰« k = 0) : h.H âŸ¶ A :=
  h.Î¹ â‰« h.descQ k hk

/-- The morphism `h.Q âŸ¶ S.Xâ‚ƒ` induced by `S.g : S.Xâ‚‚ âŸ¶ S.Xâ‚ƒ` and the fact that
`h.Q` is a cokernel of `S.f : S.Xâ‚ âŸ¶ S.Xâ‚‚`. -/
def g' : h.Q âŸ¶ S.Xâ‚ƒ := h.descQ S.g S.zero

@[reassoc (attr := simp)] lemma p_g' : h.p â‰« h.g' = S.g := p_descQ _ _ _

@[reassoc (attr := simp)] lemma Î¹_g' : h.Î¹ â‰« h.g' = 0 := h.wÎ¹

@[reassoc]
lemma Î¹_descQ_eq_zero_of_boundary (k : S.Xâ‚‚ âŸ¶ A) (x : S.Xâ‚ƒ âŸ¶ A) (hx : k = S.g â‰« x) :
    h.Î¹ â‰« h.descQ k (by rw [hx, S.zero_assoc, zero_comp]) = 0 := by
  rw [show 0 = h.Î¹ â‰« h.g' â‰« x by simp]
  congr 1
  simp only [â† cancel_epi h.p, hx, p_descQ, p_g'_assoc]

/-- For `h : S.RightHomologyData`, this is a restatement of `h.hÎ¹ `, saying that
`Î¹ : h.H âŸ¶ h.Q` is a kernel of `h.g' : h.Q âŸ¶ S.Xâ‚ƒ`. -/
def hÎ¹' : IsLimit (KernelFork.ofÎ¹ h.Î¹ h.Î¹_g') := h.hÎ¹

/-- The morphism `A âŸ¶ H` induced by a morphism `k : A âŸ¶ Q` such that `k â‰« g' = 0` -/
def liftH (k : A âŸ¶ h.Q) (hk : k â‰« h.g' = 0) : A âŸ¶ h.H :=
  h.hÎ¹.lift (KernelFork.ofÎ¹ k hk)

@[reassoc (attr := simp)]
lemma liftH_Î¹ (k : A âŸ¶ h.Q) (hk : k â‰« h.g' = 0) : h.liftH k hk â‰« h.Î¹ = k :=
  h.hÎ¹.fac (KernelFork.ofÎ¹ k hk) WalkingParallelPair.zero

lemma isIso_p (hf : S.f = 0) : IsIso h.p :=
  âŸ¨h.descQ (ğŸ™ S.Xâ‚‚) (by rw [hf, comp_id]), p_descQ _ _ _, by
    simp only [â† cancel_epi h.p, p_descQ_assoc, id_comp, comp_id]âŸ©

lemma isIso_Î¹ (hg : S.g = 0) : IsIso h.Î¹ := by
  have âŸ¨Ï†, hÏ†âŸ© := KernelFork.IsLimit.lift' h.hÎ¹' (ğŸ™ _)
    (by rw [â† cancel_epi h.p, id_comp, p_g', comp_zero, hg])
  dsimp at hÏ†
  exact âŸ¨Ï†, by rw [â† cancel_mono h.Î¹, assoc, hÏ†, comp_id, id_comp], hÏ†âŸ©

variable (S)

/-- When the first map `S.f` is zero, this is the right homology data on `S` given
by any limit kernel fork of `S.g` -/
@[simps]
def ofIsLimitKernelFork (hf : S.f = 0) (c : KernelFork S.g) (hc : IsLimit c) :
  S.RightHomologyData where
  Q := S.Xâ‚‚
  H := c.pt
  p := ğŸ™ _
  Î¹ := c.Î¹
  wp := by rw [comp_id, hf]
  hp := CokernelCofork.IsColimit.ofId _ hf
  wÎ¹ := KernelFork.condition _
  hÎ¹ := IsLimit.ofIsoLimit hc (Fork.ext (Iso.refl _) (by aesop_cat))

@[simp] lemma ofIsLimitKernelFork_g' (hf : S.f = 0) (c : KernelFork S.g)
    (hc : IsLimit c) : (ofIsLimitKernelFork S hf c hc).g' = S.g := by
  rw [â† cancel_epi (ofIsLimitKernelFork S hf c hc).p, p_g',
    ofIsLimitKernelFork_p, id_comp]

/-- When the first map `S.f` is zero, this is the right homology data on `S` given by
the chosen `kernel S.g` -/
@[simps!]
noncomputable def ofHasKernel [HasKernel S.g] (hf : S.f = 0) : S.RightHomologyData :=
ofIsLimitKernelFork S hf _ (kernelIsKernel _)

/-- When the second map `S.g` is zero, this is the right homology data on `S` given
by any colimit cokernel cofork of `S.g` -/
@[simps]
def ofIsColimitCokernelCofork (hg : S.g = 0) (c : CokernelCofork S.f) (hc : IsColimit c) :
  S.RightHomologyData where
  Q := c.pt
  H := c.pt
  p := c.Ï€
  Î¹ := ğŸ™ _
  wp := CokernelCofork.condition _
  hp := IsColimit.ofIsoColimit hc (Cofork.ext (Iso.refl _) (by aesop_cat))
  wÎ¹ := Cofork.IsColimit.hom_ext hc (by simp [hg])
  hÎ¹ := KernelFork.IsLimit.ofId _ (Cofork.IsColimit.hom_ext hc (by simp [hg]))

@[simp] lemma ofIsColimitCokernelCofork_g' (hg : S.g = 0) (c : CokernelCofork S.f)
  (hc : IsColimit c) : (ofIsColimitCokernelCofork S hg c hc).g' = 0 :=
by rw [â† cancel_epi (ofIsColimitCokernelCofork S hg c hc).p, p_g', hg, comp_zero]

/-- When the second map `S.g` is zero, this is the right homology data on `S` given
by the chosen `cokernel S.f` -/
@[simp]
noncomputable def ofHasCokernel [HasCokernel S.f] (hg : S.g = 0) : S.RightHomologyData :=
ofIsColimitCokernelCofork S hg _ (cokernelIsCokernel _)

/-- When both `S.f` and `S.g` are zero, the middle object `S.Xâ‚‚`
gives a right homology data on S -/
@[simps]
def ofZeros (hf : S.f = 0) (hg : S.g = 0) : S.RightHomologyData where
  Q := S.Xâ‚‚
  H := S.Xâ‚‚
  p := ğŸ™ _
  Î¹ := ğŸ™ _
  wp := by rw [comp_id, hf]
  hp := CokernelCofork.IsColimit.ofId _ hf
  wÎ¹ := by
    change ğŸ™ _ â‰« S.g = 0
    simp only [hg, comp_zero]
  hÎ¹ := KernelFork.IsLimit.ofId _ hg

@[simp]
lemma ofZeros_g' (hf : S.f = 0) (hg : S.g = 0) :
    (ofZeros S hf hg).g' = 0 := by
  rw [â† cancel_epi ((ofZeros S hf hg).p), comp_zero, p_g', hg]

end RightHomologyData

/-- A short complex `S` has right homology when there exists a `S.RightHomologyData` -/
class HasRightHomology : Prop where
  condition : Nonempty S.RightHomologyData

/-- A chosen `S.RightHomologyData` for a short complex `S` that has right homology -/
noncomputable def rightHomologyData [HasRightHomology S] :
  S.RightHomologyData := HasRightHomology.condition.some

variable {S}

namespace HasRightHomology

lemma mk' (h : S.RightHomologyData) : HasRightHomology S := âŸ¨Nonempty.intro hâŸ©

instance of_hasCokernel_of_hasKernel
    [HasCokernel S.f] [HasKernel (cokernel.desc S.f S.g S.zero)] :
  S.HasRightHomology := HasRightHomology.mk' (RightHomologyData.ofHasCokernelOfHasKernel S)

instance of_hasKernel {Y Z : C} (g : Y âŸ¶ Z) (X : C) [HasKernel g] :
    (ShortComplex.mk (0 : X âŸ¶ Y) g zero_comp).HasRightHomology :=
  HasRightHomology.mk' (RightHomologyData.ofHasKernel _ rfl)

instance of_hasCokernel {X Y : C} (f : X âŸ¶ Y) (Z : C) [HasCokernel f] :
    (ShortComplex.mk f (0 : Y âŸ¶ Z) comp_zero).HasRightHomology :=
  HasRightHomology.mk' (RightHomologyData.ofHasCokernel _ rfl)

instance of_zeros (X Y Z : C) :
    (ShortComplex.mk (0 : X âŸ¶ Y) (0 : Y âŸ¶ Z) zero_comp).HasRightHomology :=
  HasRightHomology.mk' (RightHomologyData.ofZeros _ rfl rfl)

end HasRightHomology

namespace RightHomologyData

/-- A right homology data for a short complex `S` induces a left homology data for `S.op`. -/
@[simps]
def op (h : S.RightHomologyData) : S.op.LeftHomologyData where
  K := Opposite.op h.Q
  H := Opposite.op h.H
  i := h.p.op
  Ï€ := h.Î¹.op
  wi := Quiver.Hom.unop_inj h.wp
  hi := CokernelCofork.IsColimit.ofÏ€Op _ _ h.hp
  wÏ€ := Quiver.Hom.unop_inj h.wÎ¹
  hÏ€ := KernelFork.IsLimit.ofÎ¹Op _ _ h.hÎ¹

@[simp] lemma op_f' (h : S.RightHomologyData) :
    h.op.f' = h.g'.op := rfl

/-- A right homology data for a short complex `S` in the opposite category
induces a left homology data for `S.unop`. -/
@[simps]
def unop {S : ShortComplex Cáµ’áµ–} (h : S.RightHomologyData) : S.unop.LeftHomologyData where
  K := Opposite.unop h.Q
  H := Opposite.unop h.H
  i := h.p.unop
  Ï€ := h.Î¹.unop
  wi := Quiver.Hom.op_inj h.wp
  hi := CokernelCofork.IsColimit.ofÏ€Unop _ _ h.hp
  wÏ€ := Quiver.Hom.op_inj h.wÎ¹
  hÏ€ := KernelFork.IsLimit.ofÎ¹Unop _ _ h.hÎ¹

@[simp] lemma unop_f' {S : ShortComplex Cáµ’áµ–} (h : S.RightHomologyData) :
    h.unop.f' = h.g'.unop := rfl

end RightHomologyData

namespace LeftHomologyData

/-- A left homology data for a short complex `S` induces a right homology data for `S.op`. -/
@[simps]
def op (h : S.LeftHomologyData) : S.op.RightHomologyData where
  Q := Opposite.op h.K
  H := Opposite.op h.H
  p := h.i.op
  Î¹ := h.Ï€.op
  wp := Quiver.Hom.unop_inj h.wi
  hp := KernelFork.IsLimit.ofÎ¹Op _ _ h.hi
  wÎ¹ := Quiver.Hom.unop_inj h.wÏ€
  hÎ¹ := CokernelCofork.IsColimit.ofÏ€Op _ _ h.hÏ€

@[simp] lemma op_g' (h : S.LeftHomologyData) :
    h.op.g' = h.f'.op := rfl

/-- A left homology data for a short complex `S` in the opposite category
induces a right homology data for `S.unop`. -/
@[simps]
def unop {S : ShortComplex Cáµ’áµ–} (h : S.LeftHomologyData) : S.unop.RightHomologyData where
  Q := Opposite.unop h.K
  H := Opposite.unop h.H
  p := h.i.unop
  Î¹ := h.Ï€.unop
  wp := Quiver.Hom.op_inj h.wi
  hp := KernelFork.IsLimit.ofÎ¹Unop _ _ h.hi
  wÎ¹ := Quiver.Hom.op_inj h.wÏ€
  hÎ¹ := CokernelCofork.IsColimit.ofÏ€Unop _ _ h.hÏ€

@[simp] lemma unop_g' {S : ShortComplex Cáµ’áµ–} (h : S.LeftHomologyData) :
    h.unop.g' = h.f'.unop := rfl

end LeftHomologyData

instance [S.HasLeftHomology] : HasRightHomology S.op :=
  HasRightHomology.mk' S.leftHomologyData.op

instance [S.HasRightHomology] : HasLeftHomology S.op :=
  HasLeftHomology.mk' S.rightHomologyData.op

lemma hasLeftHomology_iff_op (S : ShortComplex C) :
    S.HasLeftHomology â†” S.op.HasRightHomology :=
  âŸ¨fun _ => inferInstance, fun _ => HasLeftHomology.mk' S.op.rightHomologyData.unopâŸ©

lemma hasRightHomology_iff_op (S : ShortComplex C) :
    S.HasRightHomology â†” S.op.HasLeftHomology :=
  âŸ¨fun _ => inferInstance, fun _ => HasRightHomology.mk' S.op.leftHomologyData.unopâŸ©

lemma hasLeftHomology_iff_unop (S : ShortComplex Cáµ’áµ–) :
    S.HasLeftHomology â†” S.unop.HasRightHomology :=
  S.unop.hasRightHomology_iff_op.symm

lemma hasRightHomology_iff_unop (S : ShortComplex Cáµ’áµ–) :
    S.HasRightHomology â†” S.unop.HasLeftHomology :=
  S.unop.hasLeftHomology_iff_op.symm

section

variable (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (hâ‚ : Sâ‚.RightHomologyData) (hâ‚‚ : Sâ‚‚.RightHomologyData)

/-- Given right homology data `hâ‚` and `hâ‚‚` for two short complexes `Sâ‚` and `Sâ‚‚`,
a `RightHomologyMapData` for a morphism `Ï† : Sâ‚ âŸ¶ Sâ‚‚`
consists of a description of the induced morphisms on the `Q` (opcycles)
and `H` (right homology) fields of `hâ‚` and `hâ‚‚`. -/
structure RightHomologyMapData where
  /-- the induced map on opcycles -/
  Ï†Q : hâ‚.Q âŸ¶ hâ‚‚.Q
  /-- the induced map on right homology -/
  Ï†H : hâ‚.H âŸ¶ hâ‚‚.H
  /-- commutation with `p` -/
  commp : hâ‚.p â‰« Ï†Q = Ï†.Ï„â‚‚ â‰« hâ‚‚.p := by aesop_cat
  /-- commutation with `g'` -/
  commg' : Ï†Q â‰« hâ‚‚.g' = hâ‚.g' â‰« Ï†.Ï„â‚ƒ := by aesop_cat
  /-- commutation with `Î¹` -/
  commÎ¹ : Ï†H â‰« hâ‚‚.Î¹ = hâ‚.Î¹ â‰« Ï†Q := by aesop_cat

namespace RightHomologyMapData

attribute [reassoc (attr := simp)] commp commg' commÎ¹
attribute [nolint simpNF] mk.injEq

/-- The right homology map data associated to the zero morphism between two short complexes. -/
@[simps]
def zero (hâ‚ : Sâ‚.RightHomologyData) (hâ‚‚ : Sâ‚‚.RightHomologyData) :
  RightHomologyMapData 0 hâ‚ hâ‚‚ where
  Ï†Q := 0
  Ï†H := 0

/-- The right homology map data associated to the identity morphism of a short complex. -/
@[simps]
def id (h : S.RightHomologyData) : RightHomologyMapData (ğŸ™ S) h h where
  Ï†Q := ğŸ™ _
  Ï†H := ğŸ™ _

/-- The composition of right homology map data. -/
@[simps]
def comp {Ï† : Sâ‚ âŸ¶ Sâ‚‚} {Ï†' : Sâ‚‚ âŸ¶ Sâ‚ƒ} {hâ‚ : Sâ‚.RightHomologyData}
    {hâ‚‚ : Sâ‚‚.RightHomologyData} {hâ‚ƒ : Sâ‚ƒ.RightHomologyData}
    (Ïˆ : RightHomologyMapData Ï† hâ‚ hâ‚‚) (Ïˆ' : RightHomologyMapData Ï†' hâ‚‚ hâ‚ƒ) :
    RightHomologyMapData (Ï† â‰« Ï†') hâ‚ hâ‚ƒ where
  Ï†Q := Ïˆ.Ï†Q â‰« Ïˆ'.Ï†Q
  Ï†H := Ïˆ.Ï†H â‰« Ïˆ'.Ï†H

instance : Subsingleton (RightHomologyMapData Ï† hâ‚ hâ‚‚) :=
  âŸ¨fun Ïˆâ‚ Ïˆâ‚‚ => by
    have hQ : Ïˆâ‚.Ï†Q = Ïˆâ‚‚.Ï†Q := by rw [â† cancel_epi hâ‚.p, commp, commp]
    have hH : Ïˆâ‚.Ï†H = Ïˆâ‚‚.Ï†H := by rw [â† cancel_mono hâ‚‚.Î¹, commÎ¹, commÎ¹, hQ]
    cases Ïˆâ‚
    cases Ïˆâ‚‚
    congrâŸ©

instance : Inhabited (RightHomologyMapData Ï† hâ‚ hâ‚‚) := âŸ¨by
  let Ï†Q : hâ‚.Q âŸ¶ hâ‚‚.Q := hâ‚.descQ (Ï†.Ï„â‚‚ â‰« hâ‚‚.p) (by rw [â† Ï†.commâ‚â‚‚_assoc, hâ‚‚.wp, comp_zero])
  have commg' : Ï†Q â‰« hâ‚‚.g' = hâ‚.g' â‰« Ï†.Ï„â‚ƒ :=
    by rw [â† cancel_epi hâ‚.p, RightHomologyData.p_descQ_assoc, assoc,
      RightHomologyData.p_g', Ï†.commâ‚‚â‚ƒ, RightHomologyData.p_g'_assoc]
  let Ï†H : hâ‚.H âŸ¶ hâ‚‚.H := hâ‚‚.liftH (hâ‚.Î¹ â‰« Ï†Q)
    (by rw [assoc, commg', RightHomologyData.Î¹_g'_assoc, zero_comp])
  exact âŸ¨Ï†Q, Ï†H, by simp, commg', by simpâŸ©âŸ©

instance : Unique (RightHomologyMapData Ï† hâ‚ hâ‚‚) := Unique.mk' _

variable {Ï† hâ‚ hâ‚‚}

lemma congr_Ï†H {Î³â‚ Î³â‚‚ : RightHomologyMapData Ï† hâ‚ hâ‚‚} (eq : Î³â‚ = Î³â‚‚) : Î³â‚.Ï†H = Î³â‚‚.Ï†H := by rw [eq]
lemma congr_Ï†Q {Î³â‚ Î³â‚‚ : RightHomologyMapData Ï† hâ‚ hâ‚‚} (eq : Î³â‚ = Î³â‚‚) : Î³â‚.Ï†Q = Î³â‚‚.Ï†Q := by rw [eq]

end RightHomologyMapData

end

end ShortComplex

end CategoryTheory
