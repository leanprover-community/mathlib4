/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Algebra.Homology.Single

#align_import algebra.homology.augment from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Augmentation and truncation of `â„•`-indexed (co)chain complexes.
-/


noncomputable section

open CategoryTheory Limits HomologicalComplex

universe v u

variable {V : Type u} [Category.{v} V]

namespace ChainComplex

/-- The truncation of an `â„•`-indexed chain complex,
deleting the object at `0` and shifting everything else down.
-/
@[simps]
def truncate [HasZeroMorphisms V] : ChainComplex V â„• â¥¤ ChainComplex V â„• where
  obj C :=
    { X := fun i => C.X (i + 1)
      d := fun i j => C.d (i + 1) (j + 1)
      shape := fun i j w => C.shape _ _ <| by simpa }
  map f := { f := fun i => f.f (i + 1) }
#align chain_complex.truncate ChainComplex.truncate

/-- There is a canonical chain map from the truncation of a chain map `C` to
the "single object" chain complex consisting of the truncated object `C.X 0` in degree 0.
The components of this chain map are `C.d 1 0` in degree 0, and zero otherwise.
-/
def truncateTo [HasZeroObject V] [HasZeroMorphisms V] (C : ChainComplex V â„•) :
    truncate.obj C âŸ¶ (singleâ‚€ V).obj (C.X 0) :=
  (toSingleâ‚€Equiv (truncate.obj C) (C.X 0)).symm âŸ¨C.d 1 0, by aesopâŸ©
#align chain_complex.truncate_to ChainComplex.truncateTo

-- PROJECT when `V` is abelian (but not generally?)
-- `[âˆ€ n, Exact (C.d (n+2) (n+1)) (C.d (n+1) n)] [Epi (C.d 1 0)]` iff `QuasiIso (C.truncate_to)`
variable [HasZeroMorphisms V]

/-- We can "augment" a chain complex by inserting an arbitrary object in degree zero
(shifting everything else up), along with a suitable differential.
-/
def augment (C : ChainComplex V â„•) {X : V} (f : C.X 0 âŸ¶ X) (w : C.d 1 0 â‰« f = 0) :
    ChainComplex V â„• where
  X | 0 => X
    | i + 1 => C.X i
  d | 1, 0 => f
    | i + 1, j + 1 => C.d i j
    | _, _ => 0
  shape
    | 1, 0, h => absurd rfl h
    | i + 2, 0, _ => rfl
    | 0, _, _ => rfl
    | i + 1, j + 1, h => by
      simp only; exact C.shape i j (Nat.succ_ne_succ.1 h)
  d_comp_d'
    | _, _, 0, rfl, rfl => w
    | _, _, k + 1, rfl, rfl => C.d_comp_d _ _ _
#align chain_complex.augment ChainComplex.augment

@[simp]
theorem augment_X_zero (C : ChainComplex V â„•) {X : V} (f : C.X 0 âŸ¶ X) (w : C.d 1 0 â‰« f = 0) :
    (augment C f w).X 0 = X :=
  rfl
set_option linter.uppercaseLean3 false in
#align chain_complex.augment_X_zero ChainComplex.augment_X_zero

@[simp]
theorem augment_X_succ (C : ChainComplex V â„•) {X : V} (f : C.X 0 âŸ¶ X) (w : C.d 1 0 â‰« f = 0)
    (i : â„•) : (augment C f w).X (i + 1) = C.X i :=
  rfl
set_option linter.uppercaseLean3 false in
#align chain_complex.augment_X_succ ChainComplex.augment_X_succ

@[simp]
theorem augment_d_one_zero (C : ChainComplex V â„•) {X : V} (f : C.X 0 âŸ¶ X) (w : C.d 1 0 â‰« f = 0) :
    (augment C f w).d 1 0 = f :=
  rfl
#align chain_complex.augment_d_one_zero ChainComplex.augment_d_one_zero

@[simp]
theorem augment_d_succ_succ (C : ChainComplex V â„•) {X : V} (f : C.X 0 âŸ¶ X) (w : C.d 1 0 â‰« f = 0)
    (i j : â„•) : (augment C f w).d (i + 1) (j + 1) = C.d i j := by
  cases i <;> rfl
#align chain_complex.augment_d_succ_succ ChainComplex.augment_d_succ_succ

/-- Truncating an augmented chain complex is isomorphic (with components the identity)
to the original complex.
-/
def truncateAugment (C : ChainComplex V â„•) {X : V} (f : C.X 0 âŸ¶ X) (w : C.d 1 0 â‰« f = 0) :
    truncate.obj (augment C f w) â‰… C where
  hom := { f := fun i => ğŸ™ _ }
  inv :=
    { f := fun i => ğŸ™ _
      comm' := fun i j => by
        cases j <;>
          Â· dsimp
            simp }
  hom_inv_id := by
    ext (_ | i) <;>
      Â· dsimp
        simp
  inv_hom_id := by
    ext (_ | i) <;>
      Â· dsimp
        simp
#align chain_complex.truncate_augment ChainComplex.truncateAugment

@[simp]
theorem truncateAugment_hom_f (C : ChainComplex V â„•) {X : V} (f : C.X 0 âŸ¶ X) (w : C.d 1 0 â‰« f = 0)
    (i : â„•) : (truncateAugment C f w).hom.f i = ğŸ™ (C.X i) :=
  rfl
#align chain_complex.truncate_augment_hom_f ChainComplex.truncateAugment_hom_f

@[simp]
theorem truncateAugment_inv_f (C : ChainComplex V â„•) {X : V} (f : C.X 0 âŸ¶ X) (w : C.d 1 0 â‰« f = 0)
    (i : â„•) : (truncateAugment C f w).inv.f i = ğŸ™ ((truncate.obj (augment C f w)).X i) :=
  rfl
#align chain_complex.truncate_augment_inv_f ChainComplex.truncateAugment_inv_f

@[simp]
theorem chainComplex_d_succ_succ_zero (C : ChainComplex V â„•) (i : â„•) : C.d (i + 2) 0 = 0 := by
  rw [C.shape]
  exact i.succ_succ_ne_one.symm
#align chain_complex.chain_complex_d_succ_succ_zero ChainComplex.chainComplex_d_succ_succ_zero

/-- Augmenting a truncated complex with the original object and morphism is isomorphic
(with components the identity) to the original complex.
-/
def augmentTruncate (C : ChainComplex V â„•) :
    augment (truncate.obj C) (C.d 1 0) (C.d_comp_d _ _ _) â‰… C where
  hom :=
    { f := fun | 0 => ğŸ™ _ | n+1 => ğŸ™ _
      comm' := fun i j => by
        -- Porting note: was an rcases n with (_|_|n) but that was causing issues
        match i with
        | 0 | 1 | n+2 =>
          cases' j with j <;> dsimp [augment, truncate] <;> simp
    }
  inv :=
    { f := fun | 0 => ğŸ™ _ | n+1 => ğŸ™ _
      comm' := fun i j => by
        -- Porting note: was an rcases n with (_|_|n) but that was causing issues
        match i with
          | 0 | 1 | n+2 =>
          cases' j with j <;> dsimp [augment, truncate] <;> simp
    }
  hom_inv_id := by
    ext i
    cases i <;>
      Â· dsimp
        simp
  inv_hom_id := by
    ext i
    cases i <;>
      Â· dsimp
        simp
#align chain_complex.augment_truncate ChainComplex.augmentTruncate

@[simp]
theorem augmentTruncate_hom_f_zero (C : ChainComplex V â„•) :
    (augmentTruncate C).hom.f 0 = ğŸ™ (C.X 0) :=
  rfl
#align chain_complex.augment_truncate_hom_f_zero ChainComplex.augmentTruncate_hom_f_zero

@[simp]
theorem augmentTruncate_hom_f_succ (C : ChainComplex V â„•) (i : â„•) :
    (augmentTruncate C).hom.f (i + 1) = ğŸ™ (C.X (i + 1)) :=
  rfl
#align chain_complex.augment_truncate_hom_f_succ ChainComplex.augmentTruncate_hom_f_succ

@[simp]
theorem augmentTruncate_inv_f_zero (C : ChainComplex V â„•) :
    (augmentTruncate C).inv.f 0 = ğŸ™ (C.X 0) :=
  rfl
#align chain_complex.augment_truncate_inv_f_zero ChainComplex.augmentTruncate_inv_f_zero

@[simp]
theorem augmentTruncate_inv_f_succ (C : ChainComplex V â„•) (i : â„•) :
    (augmentTruncate C).inv.f (i + 1) = ğŸ™ (C.X (i + 1)) :=
  rfl
#align chain_complex.augment_truncate_inv_f_succ ChainComplex.augmentTruncate_inv_f_succ

/-- A chain map from a chain complex to a single object chain complex in degree zero
can be reinterpreted as a chain complex.

This is the inverse construction of `truncateTo`.
-/
def toSingleâ‚€AsComplex [HasZeroObject V] (C : ChainComplex V â„•) (X : V)
    (f : C âŸ¶ (singleâ‚€ V).obj X) : ChainComplex V â„• :=
  let âŸ¨f, wâŸ© := toSingleâ‚€Equiv C X f
  augment C f w
#align chain_complex.to_singleâ‚€_as_complex ChainComplex.toSingleâ‚€AsComplex

end ChainComplex

namespace CochainComplex

/-- The truncation of an `â„•`-indexed cochain complex,
deleting the object at `0` and shifting everything else down.
-/
@[simps]
def truncate [HasZeroMorphisms V] : CochainComplex V â„• â¥¤ CochainComplex V â„• where
  obj C :=
    { X := fun i => C.X (i + 1)
      d := fun i j => C.d (i + 1) (j + 1)
      shape := fun i j w => by
        apply C.shape
        simpa }
  map f := { f := fun i => f.f (i + 1) }
#align cochain_complex.truncate CochainComplex.truncate

/-- There is a canonical chain map from the truncation of a cochain complex `C` to
the "single object" cochain complex consisting of the truncated object `C.X 0` in degree 0.
The components of this chain map are `C.d 0 1` in degree 0, and zero otherwise.
-/
def toTruncate [HasZeroObject V] [HasZeroMorphisms V] (C : CochainComplex V â„•) :
    (singleâ‚€ V).obj (C.X 0) âŸ¶ truncate.obj C :=
  (fromSingleâ‚€Equiv (truncate.obj C) (C.X 0)).symm âŸ¨C.d 0 1, by aesopâŸ©
#align cochain_complex.to_truncate CochainComplex.toTruncate

variable [HasZeroMorphisms V]

/-- We can "augment" a cochain complex by inserting an arbitrary object in degree zero
(shifting everything else up), along with a suitable differential.
-/
def augment (C : CochainComplex V â„•) {X : V} (f : X âŸ¶ C.X 0) (w : f â‰« C.d 0 1 = 0) :
    CochainComplex V â„• where
  X | 0 => X
    | i + 1 => C.X i
  d | 0, 1 => f
    | i + 1, j + 1 => C.d i j
    | _, _ => 0
  shape i j s := by
    simp? at s says simp only [ComplexShape.up_Rel] at s
    rcases j with (_ | _ | j) <;> cases i <;> try simp
    Â· contradiction
    Â· rw [C.shape]
      simp only [ComplexShape.up_Rel]
      contrapose! s
      rw [â† s]
  d_comp_d' i j k hij hjk := by
    rcases k with (_ | _ | k) <;> rcases j with (_ | _ | j) <;> cases i <;> try simp
    cases k
    Â· exact w
    Â· rw [C.shape, comp_zero]
      simp only [Nat.zero_eq, ComplexShape.up_Rel, zero_add]
      exact (Nat.one_lt_succ_succ _).ne
#align cochain_complex.augment CochainComplex.augment

@[simp]
theorem augment_X_zero (C : CochainComplex V â„•) {X : V} (f : X âŸ¶ C.X 0) (w : f â‰« C.d 0 1 = 0) :
    (augment C f w).X 0 = X :=
  rfl
set_option linter.uppercaseLean3 false in
#align cochain_complex.augment_X_zero CochainComplex.augment_X_zero

@[simp]
theorem augment_X_succ (C : CochainComplex V â„•) {X : V} (f : X âŸ¶ C.X 0) (w : f â‰« C.d 0 1 = 0)
    (i : â„•) : (augment C f w).X (i + 1) = C.X i :=
  rfl
set_option linter.uppercaseLean3 false in
#align cochain_complex.augment_X_succ CochainComplex.augment_X_succ

@[simp]
theorem augment_d_zero_one (C : CochainComplex V â„•) {X : V} (f : X âŸ¶ C.X 0) (w : f â‰« C.d 0 1 = 0) :
    (augment C f w).d 0 1 = f :=
  rfl
#align cochain_complex.augment_d_zero_one CochainComplex.augment_d_zero_one

@[simp]
theorem augment_d_succ_succ (C : CochainComplex V â„•) {X : V} (f : X âŸ¶ C.X 0) (w : f â‰« C.d 0 1 = 0)
    (i j : â„•) : (augment C f w).d (i + 1) (j + 1) = C.d i j :=
  rfl
#align cochain_complex.augment_d_succ_succ CochainComplex.augment_d_succ_succ

/-- Truncating an augmented cochain complex is isomorphic (with components the identity)
to the original complex.
-/
def truncateAugment (C : CochainComplex V â„•) {X : V} (f : X âŸ¶ C.X 0) (w : f â‰« C.d 0 1 = 0) :
    truncate.obj (augment C f w) â‰… C where
  hom := { f := fun i => ğŸ™ _ }
  inv :=
    { f := fun i => ğŸ™ _
      comm' := fun i j => by
        cases j <;>
          Â· dsimp
            simp }
  hom_inv_id := by
    ext i
    cases i <;>
      Â· dsimp
        simp
  inv_hom_id := by
    ext i
    cases i <;>
      Â· dsimp
        simp
#align cochain_complex.truncate_augment CochainComplex.truncateAugment

@[simp]
theorem truncateAugment_hom_f (C : CochainComplex V â„•) {X : V} (f : X âŸ¶ C.X 0)
    (w : f â‰« C.d 0 1 = 0) (i : â„•) : (truncateAugment C f w).hom.f i = ğŸ™ (C.X i) :=
  rfl
#align cochain_complex.truncate_augment_hom_f CochainComplex.truncateAugment_hom_f

@[simp]
theorem truncateAugment_inv_f (C : CochainComplex V â„•) {X : V} (f : X âŸ¶ C.X 0)
    (w : f â‰« C.d 0 1 = 0) (i : â„•) :
    (truncateAugment C f w).inv.f i = ğŸ™ ((truncate.obj (augment C f w)).X i) :=
  rfl
#align cochain_complex.truncate_augment_inv_f CochainComplex.truncateAugment_inv_f

@[simp]
theorem cochainComplex_d_succ_succ_zero (C : CochainComplex V â„•) (i : â„•) : C.d 0 (i + 2) = 0 := by
  rw [C.shape]
  simp only [ComplexShape.up_Rel, zero_add]
  exact (Nat.one_lt_succ_succ _).ne
#align cochain_complex.cochain_complex_d_succ_succ_zero CochainComplex.cochainComplex_d_succ_succ_zero

/-- Augmenting a truncated complex with the original object and morphism is isomorphic
(with components the identity) to the original complex.
-/
def augmentTruncate (C : CochainComplex V â„•) :
    augment (truncate.obj C) (C.d 0 1) (C.d_comp_d _ _ _) â‰… C where
  hom :=
    { f := fun | 0 => ğŸ™ _ | n+1 => ğŸ™ _
      comm' := fun i j => by
        rcases j with (_ | _ | j) <;> cases i <;>
          Â· dsimp
            -- Porting note (#10959): simp can't handle this now but aesop does
            aesop }
  inv :=
    { f := fun | 0 => ğŸ™ _ | n+1 => ğŸ™ _
      comm' := fun i j => by
        rcases j with (_ | _ | j) <;> cases' i with i <;>
          Â· dsimp
            -- Porting note (#10959): simp can't handle this now but aesop does
            aesop }
  hom_inv_id := by
    ext i
    cases i <;>
      Â· dsimp
        simp
  inv_hom_id := by
    ext i
    cases i <;>
      Â· dsimp
        simp
#align cochain_complex.augment_truncate CochainComplex.augmentTruncate

@[simp]
theorem augmentTruncate_hom_f_zero (C : CochainComplex V â„•) :
    (augmentTruncate C).hom.f 0 = ğŸ™ (C.X 0) :=
  rfl
#align cochain_complex.augment_truncate_hom_f_zero CochainComplex.augmentTruncate_hom_f_zero

@[simp]
theorem augmentTruncate_hom_f_succ (C : CochainComplex V â„•) (i : â„•) :
    (augmentTruncate C).hom.f (i + 1) = ğŸ™ (C.X (i + 1)) :=
  rfl
#align cochain_complex.augment_truncate_hom_f_succ CochainComplex.augmentTruncate_hom_f_succ

@[simp]
theorem augmentTruncate_inv_f_zero (C : CochainComplex V â„•) :
    (augmentTruncate C).inv.f 0 = ğŸ™ (C.X 0) :=
  rfl
#align cochain_complex.augment_truncate_inv_f_zero CochainComplex.augmentTruncate_inv_f_zero

@[simp]
theorem augmentTruncate_inv_f_succ (C : CochainComplex V â„•) (i : â„•) :
    (augmentTruncate C).inv.f (i + 1) = ğŸ™ (C.X (i + 1)) :=
  rfl
#align cochain_complex.augment_truncate_inv_f_succ CochainComplex.augmentTruncate_inv_f_succ

/-- A chain map from a single object cochain complex in degree zero to a cochain complex
can be reinterpreted as a cochain complex.

This is the inverse construction of `toTruncate`.
-/
def fromSingleâ‚€AsComplex [HasZeroObject V] (C : CochainComplex V â„•) (X : V)
    (f : (singleâ‚€ V).obj X âŸ¶ C) : CochainComplex V â„• :=
  let âŸ¨f, wâŸ© := fromSingleâ‚€Equiv C X f
  augment C f w
#align cochain_complex.from_singleâ‚€_as_complex CochainComplex.fromSingleâ‚€AsComplex

end CochainComplex
