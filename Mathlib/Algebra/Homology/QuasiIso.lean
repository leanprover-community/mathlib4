/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Jo√´l Riou
-/
import Mathlib.Algebra.Homology.Homotopy
import Mathlib.Algebra.Homology.SingleHomology
import Mathlib.CategoryTheory.Abelian.Homology

#align_import algebra.homology.quasi_iso from "leanprover-community/mathlib"@"956af7c76589f444f2e1313911bad16366ea476d"

/-!
# Quasi-isomorphisms

A chain map is a quasi-isomorphism if it induces isomorphisms on homology.

## Future work

Define the derived category as the localization at quasi-isomorphisms? (TODO @joelriou)
-/


open CategoryTheory Limits

universe v u

variable {Œπ : Type*}

section

variable {V : Type u} [Category.{v} V] [HasZeroMorphisms V] [HasZeroObject V]
variable [HasEqualizers V] [HasImages V] [HasImageMaps V] [HasCokernels V]
variable {c : ComplexShape Œπ} {C D E : HomologicalComplex V c}

/-- A chain map is a quasi-isomorphism if it induces isomorphisms on homology.
-/
class QuasiIso' (f : C ‚ü∂ D) : Prop where
  isIso : ‚àÄ i, IsIso ((homology'Functor V c i).map f)
#align quasi_iso QuasiIso'

attribute [instance] QuasiIso'.isIso

instance (priority := 100) quasiIso'_of_iso (f : C ‚ü∂ D) [IsIso f] : QuasiIso' f where
  isIso i := by
    change IsIso ((homology'Functor V c i).mapIso (asIso f)).hom
    infer_instance
#align quasi_iso_of_iso quasiIso'_of_iso

instance quasiIso'_comp (f : C ‚ü∂ D) [QuasiIso' f] (g : D ‚ü∂ E) [QuasiIso' g] :
    QuasiIso' (f ‚â´ g) where
  isIso i := by
    rw [Functor.map_comp]
    infer_instance
#align quasi_iso_comp quasiIso'_comp

theorem quasiIso'_of_comp_left (f : C ‚ü∂ D) [QuasiIso' f] (g : D ‚ü∂ E) [QuasiIso' (f ‚â´ g)] :
    QuasiIso' g :=
  { isIso := fun i => IsIso.of_isIso_fac_left ((homology'Functor V c i).map_comp f g).symm }
#align quasi_iso_of_comp_left quasiIso'_of_comp_left

theorem quasiIso'_of_comp_right (f : C ‚ü∂ D) (g : D ‚ü∂ E) [QuasiIso' g] [QuasiIso' (f ‚â´ g)] :
    QuasiIso' f :=
  { isIso := fun i => IsIso.of_isIso_fac_right ((homology'Functor V c i).map_comp f g).symm }
#align quasi_iso_of_comp_right quasiIso'_of_comp_right

namespace HomotopyEquiv

section

variable {W : Type*} [Category W] [Preadditive W] [HasCokernels W] [HasImages W] [HasEqualizers W]
  [HasZeroObject W] [HasImageMaps W]

/-- A homotopy equivalence is a quasi-isomorphism. -/
theorem toQuasiIso' {C D : HomologicalComplex W c} (e : HomotopyEquiv C D) : QuasiIso' e.hom :=
  ‚ü®fun i => by
    refine ‚ü®‚ü®(homology'Functor W c i).map e.inv, ?_‚ü©‚ü©
    simp only [‚Üê Functor.map_comp, ‚Üê (homology'Functor W c i).map_id]
    constructor <;> apply homology'_map_eq_of_homotopy
    exacts [e.homotopyHomInvId, e.homotopyInvHomId]‚ü©
#align homotopy_equiv.to_quasi_iso HomotopyEquiv.toQuasiIso'

theorem toQuasiIso'_inv {C D : HomologicalComplex W c} (e : HomotopyEquiv C D) (i : Œπ) :
    (@asIso _ _ _ _ _ (e.toQuasiIso'.1 i)).inv = (homology'Functor W c i).map e.inv := by
  symm
  haveI := e.toQuasiIso'.1 i -- Porting note: Added this to get `asIso_hom` to work.
  simp only [‚Üê Iso.hom_comp_eq_id, asIso_hom, ‚Üê Functor.map_comp,
    ‚Üê (homology'Functor W c i).map_id, homology'_map_eq_of_homotopy e.homotopyHomInvId _]
#align homotopy_equiv.to_quasi_iso_inv HomotopyEquiv.toQuasiIso'_inv

end

end HomotopyEquiv

namespace HomologicalComplex.Hom

section ToSingle‚ÇÄ

variable {W : Type*} [Category W] [Abelian W]

section

variable {X : ChainComplex W ‚Ñï} {Y : W} (f : X ‚ü∂ (ChainComplex.single‚ÇÄ _).obj Y) [hf : QuasiIso' f]

/-- If a chain map `f : X ‚ü∂ Y[0]` is a quasi-isomorphism, then the cokernel of the differential
`d : X‚ÇÅ ‚Üí X‚ÇÄ` is isomorphic to `Y`. -/
noncomputable def toSingle‚ÇÄCokernelAtZeroIso : cokernel (X.d 1 0) ‚âÖ Y :=
  X.homology'ZeroIso.symm.trans
    ((@asIso _ _ _ _ _ (hf.1 0)).trans ((ChainComplex.homology'Functor0Single‚ÇÄ W).app Y))
#align homological_complex.hom.to_single‚ÇÄ_cokernel_at_zero_iso HomologicalComplex.Hom.toSingle‚ÇÄCokernelAtZeroIso

theorem toSingle‚ÇÄCokernelAtZeroIso_hom_eq [hf : QuasiIso' f] :
    f.toSingle‚ÇÄCokernelAtZeroIso.hom =
      cokernel.desc (X.d 1 0) (f.f 0) (by rw [‚Üê f.2 1 0 rfl]; exact comp_zero) := by
  ext
  dsimp only [toSingle‚ÇÄCokernelAtZeroIso, ChainComplex.homology'ZeroIso, homology'OfZeroRight,
    homology'.mapIso, ChainComplex.homology'Functor0Single‚ÇÄ, cokernel.map]
  dsimp [asIso]
  simp only [cokernel.œÄ_desc, Category.assoc, homology'.map_desc, cokernel.œÄ_desc_assoc]
  simp [homology'.desc, Iso.refl_inv (X.X 0)]
#align homological_complex.hom.to_single‚ÇÄ_cokernel_at_zero_iso_hom_eq HomologicalComplex.Hom.toSingle‚ÇÄCokernelAtZeroIso_hom_eq

theorem to_single‚ÇÄ_epi_at_zero [hf : QuasiIso' f] : Epi (f.f 0) := by
  constructor
  intro Z g h Hgh
  rw [‚Üê cokernel.œÄ_desc (X.d 1 0) (f.f 0) (by rw [‚Üê f.2 1 0 rfl]; exact comp_zero),
    ‚Üê toSingle‚ÇÄCokernelAtZeroIso_hom_eq] at Hgh
  rw [(@cancel_epi _ _ _ _ _ _ (epi_comp _ _) _ _).1 Hgh]
#align homological_complex.hom.to_single‚ÇÄ_epi_at_zero HomologicalComplex.Hom.to_single‚ÇÄ_epi_at_zero

theorem to_single‚ÇÄ_exact_d_f_at_zero [hf : QuasiIso' f] : Exact (X.d 1 0) (f.f 0) := by
  rw [Preadditive.exact_iff_homology'_zero]
  have h : X.d 1 0 ‚â´ f.f 0 = 0 := by simp only [‚Üê f.comm 1 0, single_obj_d, comp_zero]
  refine ‚ü®h, Nonempty.intro (homology'IsoKernelDesc _ _ _ ‚â™‚â´ ?_)‚ü©
  suffices IsIso (cokernel.desc _ _ h) by apply kernel.ofMono
  rw [‚Üê toSingle‚ÇÄCokernelAtZeroIso_hom_eq]
  infer_instance
#align homological_complex.hom.to_single‚ÇÄ_exact_d_f_at_zero HomologicalComplex.Hom.to_single‚ÇÄ_exact_d_f_at_zero

theorem to_single‚ÇÄ_exact_at_succ [hf : QuasiIso' f] (n : ‚Ñï) :
    Exact (X.d (n + 2) (n + 1)) (X.d (n + 1) n) :=
  (Preadditive.exact_iff_homology'_zero _ _).2
    ‚ü®X.d_comp_d _ _ _,
      ‚ü®(ChainComplex.homology'SuccIso _ _).symm.trans
          ((@asIso _ _ _ _ _ (hf.1 (n + 1))).trans homology'ZeroZero)‚ü©‚ü©
#align homological_complex.hom.to_single‚ÇÄ_exact_at_succ HomologicalComplex.Hom.to_single‚ÇÄ_exact_at_succ

end

section

variable {X : CochainComplex W ‚Ñï} {Y : W} (f : (CochainComplex.single‚ÇÄ _).obj Y ‚ü∂ X)

/-- If a cochain map `f : Y[0] ‚ü∂ X` is a quasi-isomorphism, then the kernel of the differential
`d : X‚ÇÄ ‚Üí X‚ÇÅ` is isomorphic to `Y`. -/
noncomputable def fromSingle‚ÇÄKernelAtZeroIso [hf : QuasiIso' f] : kernel (X.d 0 1) ‚âÖ Y :=
  X.homology'ZeroIso.symm.trans
    ((@asIso _ _ _ _ _ (hf.1 0)).symm.trans ((CochainComplex.homologyFunctor0Single‚ÇÄ W).app Y))
#align homological_complex.hom.from_single‚ÇÄ_kernel_at_zero_iso HomologicalComplex.Hom.fromSingle‚ÇÄKernelAtZeroIso

theorem fromSingle‚ÇÄKernelAtZeroIso_inv_eq [hf : QuasiIso' f] :
    f.fromSingle‚ÇÄKernelAtZeroIso.inv =
      kernel.lift (X.d 0 1) (f.f 0) (by rw [f.2 0 1 rfl]; exact zero_comp) := by
  ext
  dsimp only [fromSingle‚ÇÄKernelAtZeroIso, CochainComplex.homology'ZeroIso, homology'OfZeroLeft,
    homology'.mapIso, CochainComplex.homologyFunctor0Single‚ÇÄ, kernel.map]
  simp only [Iso.trans_inv, Iso.app_inv, Iso.symm_inv, Category.assoc, equalizer_as_kernel,
    kernel.lift_Œπ]
  dsimp [asIso]
  simp only [Category.assoc, homology'.œÄ_map, cokernelZeroIsoTarget_hom,
    cokernelIsoOfEq_hom_comp_desc, kernelSubobject_arrow, homology'.œÄ_map_assoc, IsIso.inv_comp_eq]
  simp [homology'.œÄ, kernelSubobjectMap_comp, Iso.refl_hom (X.X 0), Category.comp_id]
#align homological_complex.hom.from_single‚ÇÄ_kernel_at_zero_iso_inv_eq HomologicalComplex.Hom.fromSingle‚ÇÄKernelAtZeroIso_inv_eq

theorem from_single‚ÇÄ_mono_at_zero [hf : QuasiIso' f] : Mono (f.f 0) := by
  constructor
  intro Z g h Hgh
  rw [‚Üê kernel.lift_Œπ (X.d 0 1) (f.f 0) (by rw [f.2 0 1 rfl]; exact zero_comp),
    ‚Üê fromSingle‚ÇÄKernelAtZeroIso_inv_eq] at Hgh
  rw [(@cancel_mono _ _ _ _ _ _ (mono_comp _ _) _ _).1 Hgh]
#align homological_complex.hom.from_single‚ÇÄ_mono_at_zero HomologicalComplex.Hom.from_single‚ÇÄ_mono_at_zero

theorem from_single‚ÇÄ_exact_f_d_at_zero [hf : QuasiIso' f] : Exact (f.f 0) (X.d 0 1) := by
  rw [Preadditive.exact_iff_homology'_zero]
  have h : f.f 0 ‚â´ X.d 0 1 = 0 := by simp
  refine ‚ü®h, Nonempty.intro (homology'IsoCokernelLift _ _ _ ‚â™‚â´ ?_)‚ü©
  suffices IsIso (kernel.lift (X.d 0 1) (f.f 0) h) by apply cokernel.ofEpi
  rw [‚Üê fromSingle‚ÇÄKernelAtZeroIso_inv_eq f]
  infer_instance
#align homological_complex.hom.from_single‚ÇÄ_exact_f_d_at_zero HomologicalComplex.Hom.from_single‚ÇÄ_exact_f_d_at_zero

theorem from_single‚ÇÄ_exact_at_succ [hf : QuasiIso' f] (n : ‚Ñï) :
    Exact (X.d n (n + 1)) (X.d (n + 1) (n + 2)) :=
  (Preadditive.exact_iff_homology'_zero _ _).2
    ‚ü®X.d_comp_d _ _ _,
      ‚ü®(CochainComplex.homology'SuccIso _ _).symm.trans
          ((@asIso _ _ _ _ _ (hf.1 (n + 1))).symm.trans homology'ZeroZero)‚ü©‚ü©
#align homological_complex.hom.from_single‚ÇÄ_exact_at_succ HomologicalComplex.Hom.from_single‚ÇÄ_exact_at_succ

end

end ToSingle‚ÇÄ

end HomologicalComplex.Hom

variable {A : Type*} [Category A] [Abelian A] {B : Type*} [Category B] [Abelian B] (F : A ‚•§ B)
  [Functor.Additive F] [PreservesFiniteLimits F] [PreservesFiniteColimits F] [F.Faithful]

theorem CategoryTheory.Functor.quasiIso'_of_map_quasiIso' {C D : HomologicalComplex A c}
    (f : C ‚ü∂ D) (hf : QuasiIso' ((F.mapHomologicalComplex _).map f)) : QuasiIso' f :=
  ‚ü®fun i =>
    haveI : IsIso (F.map ((homology'Functor A c i).map f)) := by
      rw [‚Üê Functor.comp_map, ‚Üê NatIso.naturality_2 (F.homology'FunctorIso i) f, Functor.comp_map]
      infer_instance
    isIso_of_reflects_iso _ F‚ü©
#align category_theory.functor.quasi_iso_of_map_quasi_iso CategoryTheory.Functor.quasiIso'_of_map_quasiIso'

end

open HomologicalComplex

section

variable {Œπ : Type*} {C : Type u} [Category.{v} C] [HasZeroMorphisms C]
  {c : ComplexShape Œπ} {K L M K' L' : HomologicalComplex C c}

/-- A morphism of homological complexes `f : K ‚ü∂ L` is a quasi-isomorphism in degree `i`
when it induces a quasi-isomorphism of short complexes `K.sc i ‚ü∂ L.sc i`. -/
class QuasiIsoAt (f : K ‚ü∂ L) (i : Œπ) [K.HasHomology i] [L.HasHomology i] : Prop where
  quasiIso : ShortComplex.QuasiIso ((shortComplexFunctor C c i).map f)

lemma quasiIsoAt_iff (f : K ‚ü∂ L) (i : Œπ) [K.HasHomology i] [L.HasHomology i] :
    QuasiIsoAt f i ‚Üî
      ShortComplex.QuasiIso ((shortComplexFunctor C c i).map f) := by
  constructor
  ¬∑ intro h
    exact h.quasiIso
  ¬∑ intro h
    exact ‚ü®h‚ü©

instance quasiIsoAt_of_isIso (f : K ‚ü∂ L) [IsIso f] (i : Œπ) [K.HasHomology i] [L.HasHomology i] :
    QuasiIsoAt f i := by
  rw [quasiIsoAt_iff]
  infer_instance

lemma quasiIsoAt_iff' (f : K ‚ü∂ L) (i j k : Œπ) (hi : c.prev j = i) (hk : c.next j = k)
    [K.HasHomology j] [L.HasHomology j] [(K.sc' i j k).HasHomology] [(L.sc' i j k).HasHomology] :
    QuasiIsoAt f j ‚Üî
      ShortComplex.QuasiIso ((shortComplexFunctor' C c i j k).map f) := by
  rw [quasiIsoAt_iff]
  exact ShortComplex.quasiIso_iff_of_arrow_mk_iso _ _
    (Arrow.isoOfNatIso (natIsoSc' C c i j k hi hk) (Arrow.mk f))

lemma quasiIsoAt_iff_isIso_homologyMap (f : K ‚ü∂ L) (i : Œπ)
    [K.HasHomology i] [L.HasHomology i] :
    QuasiIsoAt f i ‚Üî IsIso (homologyMap f i) := by
  rw [quasiIsoAt_iff, ShortComplex.quasiIso_iff]
  rfl

lemma quasiIsoAt_iff_exactAt (f : K ‚ü∂ L) (i : Œπ) [K.HasHomology i] [L.HasHomology i]
    (hK : K.ExactAt i) :
    QuasiIsoAt f i ‚Üî L.ExactAt i := by
  simp only [quasiIsoAt_iff, ShortComplex.quasiIso_iff, exactAt_iff,
    ShortComplex.exact_iff_isZero_homology] at hK ‚ä¢
  constructor
  ¬∑ intro h
    exact IsZero.of_iso hK (@asIso _ _ _ _ _ h).symm
  ¬∑ intro hL
    exact ‚ü®‚ü®0, IsZero.eq_of_src hK _ _, IsZero.eq_of_tgt hL _ _‚ü©‚ü©

lemma quasiIsoAt_iff_exactAt' (f : K ‚ü∂ L) (i : Œπ) [K.HasHomology i] [L.HasHomology i]
    (hL : L.ExactAt i) :
    QuasiIsoAt f i ‚Üî K.ExactAt i := by
  simp only [quasiIsoAt_iff, ShortComplex.quasiIso_iff, exactAt_iff,
    ShortComplex.exact_iff_isZero_homology] at hL ‚ä¢
  constructor
  ¬∑ intro h
    exact IsZero.of_iso hL (@asIso _ _ _ _ _ h)
  ¬∑ intro hK
    exact ‚ü®‚ü®0, IsZero.eq_of_src hK _ _, IsZero.eq_of_tgt hL _ _‚ü©‚ü©

instance (f : K ‚ü∂ L) (i : Œπ) [K.HasHomology i] [L.HasHomology i] [hf : QuasiIsoAt f i] :
    IsIso (homologyMap f i) := by
  simpa only [quasiIsoAt_iff, ShortComplex.quasiIso_iff] using hf

/-- The isomorphism `K.homology i ‚âÖ L.homology i` induced by a morphism `f : K ‚ü∂ L` such
that `[QuasiIsoAt f i]` holds. -/
@[simps! hom]
noncomputable def isoOfQuasiIsoAt (f : K ‚ü∂ L) (i : Œπ) [K.HasHomology i] [L.HasHomology i]
    [QuasiIsoAt f i] : K.homology i ‚âÖ L.homology i :=
  asIso (homologyMap f i)

@[reassoc (attr := simp)]
lemma isoOfQuasiIsoAt_hom_inv_id (f : K ‚ü∂ L) (i : Œπ) [K.HasHomology i] [L.HasHomology i]
    [QuasiIsoAt f i] :
    homologyMap f i ‚â´ (isoOfQuasiIsoAt f i).inv = ùüô _ :=
  (isoOfQuasiIsoAt f i).hom_inv_id

@[reassoc (attr := simp)]
lemma isoOfQuasiIsoAt_inv_hom_id (f : K ‚ü∂ L) (i : Œπ) [K.HasHomology i] [L.HasHomology i]
    [QuasiIsoAt f i] :
    (isoOfQuasiIsoAt f i).inv ‚â´ homologyMap f i = ùüô _ :=
  (isoOfQuasiIsoAt f i).inv_hom_id

lemma CochainComplex.quasiIsoAt‚ÇÄ_iff {K L : CochainComplex C ‚Ñï} (f : K ‚ü∂ L)
    [K.HasHomology 0] [L.HasHomology 0] [(K.sc' 0 0 1).HasHomology] [(L.sc' 0 0 1).HasHomology] :
    QuasiIsoAt f 0 ‚Üî
      ShortComplex.QuasiIso ((HomologicalComplex.shortComplexFunctor' C _ 0 0 1).map f) :=
  quasiIsoAt_iff' _ _ _ _ (by simp) (by simp)

lemma ChainComplex.quasiIsoAt‚ÇÄ_iff {K L : ChainComplex C ‚Ñï} (f : K ‚ü∂ L)
    [K.HasHomology 0] [L.HasHomology 0] [(K.sc' 1 0 0).HasHomology] [(L.sc' 1 0 0).HasHomology] :
    QuasiIsoAt f 0 ‚Üî
      ShortComplex.QuasiIso ((HomologicalComplex.shortComplexFunctor' C _ 1 0 0).map f) :=
  quasiIsoAt_iff' _ _ _ _ (by simp) (by simp)

/-- A morphism of homological complexes `f : K ‚ü∂ L` is a quasi-isomorphism when it
is so in every degree, i.e. when the induced maps `homologyMap f i : K.homology i ‚ü∂ L.homology i`
are all isomorphisms (see `quasiIso_iff` and `quasiIsoAt_iff_isIso_homologyMap`). -/
class QuasiIso (f : K ‚ü∂ L) [‚àÄ i, K.HasHomology i] [‚àÄ i, L.HasHomology i] : Prop where
  quasiIsoAt : ‚àÄ i, QuasiIsoAt f i := by infer_instance

lemma quasiIso_iff (f : K ‚ü∂ L) [‚àÄ i, K.HasHomology i] [‚àÄ i, L.HasHomology i] :
    QuasiIso f ‚Üî ‚àÄ i, QuasiIsoAt f i :=
  ‚ü®fun h => h.quasiIsoAt, fun h => ‚ü®h‚ü©‚ü©

attribute [instance] QuasiIso.quasiIsoAt

instance quasiIso_of_isIso (f : K ‚ü∂ L) [IsIso f] [‚àÄ i, K.HasHomology i] [‚àÄ i, L.HasHomology i] :
    QuasiIso f where

instance quasiIsoAt_comp (œÜ : K ‚ü∂ L) (œÜ' : L ‚ü∂ M) (i : Œπ) [K.HasHomology i]
    [L.HasHomology i] [M.HasHomology i]
    [hœÜ : QuasiIsoAt œÜ i] [hœÜ' : QuasiIsoAt œÜ' i] :
    QuasiIsoAt (œÜ ‚â´ œÜ') i := by
  rw [quasiIsoAt_iff] at hœÜ hœÜ' ‚ä¢
  rw [Functor.map_comp]
  exact ShortComplex.quasiIso_comp _ _

instance quasiIso_comp (œÜ : K ‚ü∂ L) (œÜ' : L ‚ü∂ M) [‚àÄ i, K.HasHomology i]
    [‚àÄ i, L.HasHomology i] [‚àÄ i, M.HasHomology i]
    [hœÜ : QuasiIso œÜ] [hœÜ' : QuasiIso œÜ'] :
    QuasiIso (œÜ ‚â´ œÜ') where

lemma quasiIsoAt_of_comp_left (œÜ : K ‚ü∂ L) (œÜ' : L ‚ü∂ M) (i : Œπ) [K.HasHomology i]
    [L.HasHomology i] [M.HasHomology i]
    [hœÜ : QuasiIsoAt œÜ i] [hœÜœÜ' : QuasiIsoAt (œÜ ‚â´ œÜ') i] :
    QuasiIsoAt œÜ' i := by
  rw [quasiIsoAt_iff_isIso_homologyMap] at hœÜ hœÜœÜ' ‚ä¢
  rw [homologyMap_comp] at hœÜœÜ'
  exact IsIso.of_isIso_comp_left (homologyMap œÜ i) (homologyMap œÜ' i)

lemma quasiIsoAt_iff_comp_left (œÜ : K ‚ü∂ L) (œÜ' : L ‚ü∂ M) (i : Œπ) [K.HasHomology i]
    [L.HasHomology i] [M.HasHomology i]
    [hœÜ : QuasiIsoAt œÜ i] :
    QuasiIsoAt (œÜ ‚â´ œÜ') i ‚Üî QuasiIsoAt œÜ' i := by
  constructor
  ¬∑ intro
    exact quasiIsoAt_of_comp_left œÜ œÜ' i
  ¬∑ intro
    infer_instance

lemma quasiIso_iff_comp_left (œÜ : K ‚ü∂ L) (œÜ' : L ‚ü∂ M) [‚àÄ i, K.HasHomology i]
    [‚àÄ i, L.HasHomology i] [‚àÄ i, M.HasHomology i]
    [hœÜ : QuasiIso œÜ] :
    QuasiIso (œÜ ‚â´ œÜ') ‚Üî QuasiIso œÜ' := by
  simp only [quasiIso_iff, quasiIsoAt_iff_comp_left œÜ œÜ']

lemma quasiIso_of_comp_left (œÜ : K ‚ü∂ L) (œÜ' : L ‚ü∂ M) [‚àÄ i, K.HasHomology i]
    [‚àÄ i, L.HasHomology i] [‚àÄ i, M.HasHomology i]
    [hœÜ : QuasiIso œÜ] [hœÜœÜ' : QuasiIso (œÜ ‚â´ œÜ')] :
    QuasiIso œÜ' := by
  rw [‚Üê quasiIso_iff_comp_left œÜ œÜ']
  infer_instance

lemma quasiIsoAt_of_comp_right (œÜ : K ‚ü∂ L) (œÜ' : L ‚ü∂ M) (i : Œπ) [K.HasHomology i]
    [L.HasHomology i] [M.HasHomology i]
    [hœÜ' : QuasiIsoAt œÜ' i] [hœÜœÜ' : QuasiIsoAt (œÜ ‚â´ œÜ') i] :
    QuasiIsoAt œÜ i := by
  rw [quasiIsoAt_iff_isIso_homologyMap] at hœÜ' hœÜœÜ' ‚ä¢
  rw [homologyMap_comp] at hœÜœÜ'
  exact IsIso.of_isIso_comp_right (homologyMap œÜ i) (homologyMap œÜ' i)

lemma quasiIsoAt_iff_comp_right (œÜ : K ‚ü∂ L) (œÜ' : L ‚ü∂ M) (i : Œπ) [K.HasHomology i]
    [L.HasHomology i] [M.HasHomology i]
    [hœÜ' : QuasiIsoAt œÜ' i] :
    QuasiIsoAt (œÜ ‚â´ œÜ') i ‚Üî QuasiIsoAt œÜ i := by
  constructor
  ¬∑ intro
    exact quasiIsoAt_of_comp_right œÜ œÜ' i
  ¬∑ intro
    infer_instance

lemma quasiIso_iff_comp_right (œÜ : K ‚ü∂ L) (œÜ' : L ‚ü∂ M) [‚àÄ i, K.HasHomology i]
    [‚àÄ i, L.HasHomology i] [‚àÄ i, M.HasHomology i]
    [hœÜ' : QuasiIso œÜ'] :
    QuasiIso (œÜ ‚â´ œÜ') ‚Üî QuasiIso œÜ := by
  simp only [quasiIso_iff, quasiIsoAt_iff_comp_right œÜ œÜ']

lemma quasiIso_of_comp_right (œÜ : K ‚ü∂ L) (œÜ' : L ‚ü∂ M) [‚àÄ i, K.HasHomology i]
    [‚àÄ i, L.HasHomology i] [‚àÄ i, M.HasHomology i]
    [hœÜ : QuasiIso œÜ'] [hœÜœÜ' : QuasiIso (œÜ ‚â´ œÜ')] :
    QuasiIso œÜ := by
  rw [‚Üê quasiIso_iff_comp_right œÜ œÜ']
  infer_instance

lemma quasiIso_iff_of_arrow_mk_iso (œÜ : K ‚ü∂ L) (œÜ' : K' ‚ü∂ L') (e : Arrow.mk œÜ ‚âÖ Arrow.mk œÜ')
    [‚àÄ i, K.HasHomology i] [‚àÄ i, L.HasHomology i]
    [‚àÄ i, K'.HasHomology i] [‚àÄ i, L'.HasHomology i] :
    QuasiIso œÜ ‚Üî QuasiIso œÜ' := by
  rw [‚Üê quasiIso_iff_comp_left (show K' ‚ü∂ K from e.inv.left) œÜ,
    ‚Üê quasiIso_iff_comp_right œÜ' (show L' ‚ü∂ L from e.inv.right)]
  erw [Arrow.w e.inv]
  rfl

lemma quasiIso_of_arrow_mk_iso (œÜ : K ‚ü∂ L) (œÜ' : K' ‚ü∂ L') (e : Arrow.mk œÜ ‚âÖ Arrow.mk œÜ')
    [‚àÄ i, K.HasHomology i] [‚àÄ i, L.HasHomology i]
    [‚àÄ i, K'.HasHomology i] [‚àÄ i, L'.HasHomology i]
    [hœÜ : QuasiIso œÜ] : QuasiIso œÜ' := by
  simpa only [‚Üê quasiIso_iff_of_arrow_mk_iso œÜ œÜ' e]

namespace HomologicalComplex

section PreservesHomology

variable {C‚ÇÅ C‚ÇÇ : Type*} [Category C‚ÇÅ] [Category C‚ÇÇ] [Preadditive C‚ÇÅ] [Preadditive C‚ÇÇ]
  {K L : HomologicalComplex C‚ÇÅ c} (œÜ : K ‚ü∂ L) (F : C‚ÇÅ ‚•§ C‚ÇÇ) [F.Additive]
  [F.PreservesHomology]

section

variable (i : Œπ) [K.HasHomology i] [L.HasHomology i]
  [((F.mapHomologicalComplex c).obj K).HasHomology i]
  [((F.mapHomologicalComplex c).obj L).HasHomology i]

instance quasiIsoAt_map_of_preservesHomology [hœÜ : QuasiIsoAt œÜ i] :
    QuasiIsoAt ((F.mapHomologicalComplex c).map œÜ) i := by
  rw [quasiIsoAt_iff] at hœÜ ‚ä¢
  exact ShortComplex.quasiIso_map_of_preservesLeftHomology F
    ((shortComplexFunctor C‚ÇÅ c i).map œÜ)

lemma quasiIsoAt_map_iff_of_preservesHomology [F.ReflectsIsomorphisms] :
    QuasiIsoAt ((F.mapHomologicalComplex c).map œÜ) i ‚Üî QuasiIsoAt œÜ i := by
  simp only [quasiIsoAt_iff]
  exact ShortComplex.quasiIso_map_iff_of_preservesLeftHomology F
    ((shortComplexFunctor C‚ÇÅ c i).map œÜ)

end

section

variable [‚àÄ i, K.HasHomology i] [‚àÄ i, L.HasHomology i]
  [‚àÄ i, ((F.mapHomologicalComplex c).obj K).HasHomology i]
  [‚àÄ i, ((F.mapHomologicalComplex c).obj L).HasHomology i]

instance quasiIso_map_of_preservesHomology [hœÜ : QuasiIso œÜ] :
    QuasiIso ((F.mapHomologicalComplex c).map œÜ) where

lemma quasiIso_map_iff_of_preservesHomology [F.ReflectsIsomorphisms] :
    QuasiIso ((F.mapHomologicalComplex c).map œÜ) ‚Üî QuasiIso œÜ := by
  simp only [quasiIso_iff, quasiIsoAt_map_iff_of_preservesHomology œÜ F]

end

end PreservesHomology

variable (C c)

/-- The morphism property on `HomologicalComplex C c` given by quasi-isomorphisms. -/
def quasiIso [CategoryWithHomology C] :
    MorphismProperty (HomologicalComplex C c) := fun _ _ f => QuasiIso f

variable {C c}

@[simp]
lemma mem_quasiIso_iff [CategoryWithHomology C] (f : K ‚ü∂ L) : quasiIso C c f ‚Üî QuasiIso f := by rfl

end HomologicalComplex

end

section

variable {Œπ : Type*} {C : Type u} [Category.{v} C] [Preadditive C]
  {c : ComplexShape Œπ} {K L : HomologicalComplex C c}

section

variable (e : HomotopyEquiv K L) [‚àÄ i, K.HasHomology i] [‚àÄ i, L.HasHomology i]

instance : QuasiIso e.hom where
  quasiIsoAt n := by
    classical
    rw [quasiIsoAt_iff_isIso_homologyMap]
    exact (e.toHomologyIso n).isIso_hom

instance : QuasiIso e.inv := (inferInstance : QuasiIso e.symm.hom)

variable (C c)

lemma homotopyEquivalences_le_quasiIso [CategoryWithHomology C] :
    homotopyEquivalences C c ‚â§ quasiIso C c := by
  rintro K L _ ‚ü®e, rfl‚ü©
  simp only [HomologicalComplex.mem_quasiIso_iff]
  infer_instance
