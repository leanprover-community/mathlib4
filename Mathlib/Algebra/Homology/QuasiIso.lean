/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Jo√´l Riou
-/
import Mathlib.Algebra.Homology.Homotopy
import Mathlib.CategoryTheory.Abelian.Homology
import Mathlib.Algebra.Homology.ShortComplex.HomologicalComplex

#align_import algebra.homology.quasi_iso from "leanprover-community/mathlib"@"956af7c76589f444f2e1313911bad16366ea476d"

/-!
# Quasi-isomorphisms

A chain map is a quasi-isomorphism if it induces isomorphisms on homology.

## Future work

Define the derived category as the localization at quasi-isomorphisms?
-/


open CategoryTheory

open CategoryTheory.Limits

universe v u

/- redundant with the new homology API

section

variable {Œπ : Type _}

variable {V : Type u} [Category.{v} V] [HasZeroMorphisms V] [HasZeroObject V]

variable [HasEqualizers V] [HasImages V] [HasImageMaps V] [HasCokernels V]

variable {c : ComplexShape Œπ} {C D E : HomologicalComplex V c}

/-- A chain map is a quasi-isomorphism if it induces isomorphisms on homology.
-/
class QuasiIso' (f : C ‚ü∂ D) : Prop where
  IsIso : ‚àÄ i, IsIso ((homology'Functor V c i).map f)
#align quasi_iso QuasiIso'

attribute [instance] QuasiIso'.IsIso

instance (priority := 100) quasiIso'_of_iso (f : C ‚ü∂ D) [IsIso f] : QuasiIso' f where
  IsIso i := by
    change IsIso ((homology'Functor V c i).mapIso (asIso f)).hom
    infer_instance
#align quasi_iso_of_iso quasiIso'_of_iso

instance quasiIso'_comp (f : C ‚ü∂ D) [QuasiIso' f] (g : D ‚ü∂ E) [QuasiIso' g] : QuasiIso' (f ‚â´ g) where
  IsIso i := by
    rw [Functor.map_comp]
    infer_instance
#align quasi_iso_comp quasiIso'_comp

theorem quasiIso'_of_comp_left (f : C ‚ü∂ D) [QuasiIso' f] (g : D ‚ü∂ E) [QuasiIso' (f ‚â´ g)] :
    QuasiIso' g :=
  { IsIso := fun i => IsIso.of_isIso_fac_left ((homology'Functor V c i).map_comp f g).symm }
#align quasi_iso_of_comp_left quasiIso'_of_comp_left

theorem quasiIso'_of_comp_right (f : C ‚ü∂ D) (g : D ‚ü∂ E) [QuasiIso' g] [QuasiIso' (f ‚â´ g)] :
    QuasiIso' f :=
  { IsIso := fun i => IsIso.of_isIso_fac_right ((homology'Functor V c i).map_comp f g).symm }
#align quasi_iso_of_comp_right quasiIso'_of_comp_right

namespace HomotopyEquiv

section

variable {W : Type*} [Category W] [Preadditive W] [HasCokernels W] [HasImages W] [HasEqualizers W]
  [HasZeroObject W] [HasImageMaps W]

/-- A homotopy equivalence is a quasi-isomorphism. -/
theorem toQuasiIso' {C D : HomologicalComplex W c} (e : HomotopyEquiv C D) : QuasiIso' e.hom :=
  ‚ü®fun i => by
    refine' ‚ü®‚ü®(homology'Functor W c i).map e.inv, _‚ü©‚ü©
    simp only [‚Üê Functor.map_comp, ‚Üê (homology'Functor W c i).map_id]
    constructor <;> apply homology_map_eq_of_homotopy
    exacts [e.homotopyHomInvId, e.homotopyInvHomId]‚ü©
#align homotopy_equiv.to_quasi_iso HomotopyEquiv.toQuasiIso'

theorem toQuasiIso'_inv {C D : HomologicalComplex W c} (e : HomotopyEquiv C D) (i : Œπ) :
    (@asIso _ _ _ _ _ (e.toQuasiIso'.1 i)).inv = (homology'Functor W c i).map e.inv := by
  symm
  haveI := e.toQuasiIso'.1 i -- Porting note: Added this to get `asIso_hom` to work.
  simp only [‚Üê Iso.hom_comp_eq_id, asIso_hom, ‚Üê Functor.map_comp, ‚Üê (homology'Functor W c i).map_id,
    homology_map_eq_of_homotopy e.homotopyHomInvId _]
#align homotopy_equiv.to_quasi_iso_inv HomotopyEquiv.toQuasiIso'_inv

end

end HomotopyEquiv

namespace HomologicalComplex.Hom

section ToSingle‚ÇÄ

variable {W : Type*} [Category W] [Abelian W]

section

variable {X : ChainComplex W ‚Ñï} {Y : W} (f : X ‚ü∂ (ChainComplex.single‚ÇÄ _).obj Y) [hf : QuasiIso' f]

/-- If a chain map `f : X ‚ü∂ Y[0]` is a quasi-isomorphism, then the cokernel of the differential
`d : X‚ÇÅ ‚Üí X‚ÇÄ` is isomorphic to `Y`. -/
noncomputable def toSingle‚ÇÄCokernelAtZeroIso : cokernel (X.d 1 0) ‚âÖ Y :=
  X.homology'ZeroIso.symm.trans
    ((@asIso _ _ _ _ _ (hf.1 0)).trans ((ChainComplex.homology'Functor0Single‚ÇÄ W).app Y))
#align homological_complex.hom.to_single‚ÇÄ_cokernel_at_zero_iso HomologicalComplex.Hom.toSingle‚ÇÄCokernelAtZeroIso

theorem toSingle‚ÇÄCokernelAtZeroIso_hom_eq [hf : QuasiIso' f] :
    f.toSingle‚ÇÄCokernelAtZeroIso.hom =
      cokernel.desc (X.d 1 0) (f.f 0) (by rw [‚Üê f.2 1 0 rfl]; exact comp_zero) := by
  ext
  dsimp only [toSingle‚ÇÄCokernelAtZeroIso, ChainComplex.homology'ZeroIso, homology'OfZeroRight,
    homology'.mapIso, ChainComplex.homology'Functor0Single‚ÇÄ, cokernel.map]
  dsimp [asIso]
  simp only [cokernel.œÄ_desc, Category.assoc, homology'.map_desc, cokernel.œÄ_desc_assoc]
  simp [homology'.desc, Iso.refl_inv (X.X 0)]
#align homological_complex.hom.to_single‚ÇÄ_cokernel_at_zero_iso_hom_eq HomologicalComplex.Hom.toSingle‚ÇÄCokernelAtZeroIso_hom_eq

theorem to_single‚ÇÄ_epi_at_zero [hf : QuasiIso' f] : Epi (f.f 0) := by
  constructor
  intro Z g h Hgh
  rw [‚Üê cokernel.œÄ_desc (X.d 1 0) (f.f 0) (by rw [‚Üê f.2 1 0 rfl]; exact comp_zero),
    ‚Üê toSingle‚ÇÄCokernelAtZeroIso_hom_eq] at Hgh
  rw [(@cancel_epi _ _ _ _ _ _ (epi_comp _ _) _ _).1 Hgh]
#align homological_complex.hom.to_single‚ÇÄ_epi_at_zero HomologicalComplex.Hom.to_single‚ÇÄ_epi_at_zero

theorem to_single‚ÇÄ_exact_d_f_at_zero [hf : QuasiIso' f] : Exact (X.d 1 0) (f.f 0) := by
  rw [Preadditive.exact_iff_homology'_zero]
  have h : X.d 1 0 ‚â´ f.f 0 = 0 := by
    simp only [‚Üê f.2 1 0 rfl, ChainComplex.single‚ÇÄ_obj_X_d, comp_zero]
  refine' ‚ü®h, Nonempty.intro (homology'IsoKernelDesc _ _ _ ‚â™‚â´ _)‚ü©
  suffices IsIso (cokernel.desc _ _ h) by apply kernel.ofMono
  rw [‚Üê toSingle‚ÇÄCokernelAtZeroIso_hom_eq]
  infer_instance
#align homological_complex.hom.to_single‚ÇÄ_exact_d_f_at_zero HomologicalComplex.Hom.to_single‚ÇÄ_exact_d_f_at_zero

theorem to_single‚ÇÄ_exact_at_succ [hf : QuasiIso' f] (n : ‚Ñï) :
    Exact (X.d (n + 2) (n + 1)) (X.d (n + 1) n) :=
  (Preadditive.exact_iff_homology'_zero _ _).2
    ‚ü®X.d_comp_d _ _ _,
      ‚ü®(ChainComplex.homology'SuccIso _ _).symm.trans
          ((@asIso _ _ _ _ _ (hf.1 (n + 1))).trans homology'ZeroZero)‚ü©‚ü©
#align homological_complex.hom.to_single‚ÇÄ_exact_at_succ HomologicalComplex.Hom.to_single‚ÇÄ_exact_at_succ

end

section

variable {X : CochainComplex W ‚Ñï} {Y : W} (f : (CochainComplex.single‚ÇÄ _).obj Y ‚ü∂ X)

/-- If a cochain map `f : Y[0] ‚ü∂ X` is a quasi-isomorphism, then the kernel of the differential
`d : X‚ÇÄ ‚Üí X‚ÇÅ` is isomorphic to `Y`. -/
noncomputable def fromSingle‚ÇÄKernelAtZeroIso [hf : QuasiIso' f] : kernel (X.d 0 1) ‚âÖ Y :=
  X.homology'ZeroIso.symm.trans
    ((@asIso _ _ _ _ _ (hf.1 0)).symm.trans ((CochainComplex.homology'Functor0Single‚ÇÄ W).app Y))
#align homological_complex.hom.from_single‚ÇÄ_kernel_at_zero_iso HomologicalComplex.Hom.fromSingle‚ÇÄKernelAtZeroIso

theorem fromSingle‚ÇÄKernelAtZeroIso_inv_eq [hf : QuasiIso' f] :
    f.fromSingle‚ÇÄKernelAtZeroIso.inv =
      kernel.lift (X.d 0 1) (f.f 0) (by rw [f.2 0 1 rfl]; exact zero_comp) := by
  ext
  dsimp only [fromSingle‚ÇÄKernelAtZeroIso, CochainComplex.homology'ZeroIso, homology'OfZeroLeft,
    homology'.mapIso, CochainComplex.homology'Functor0Single‚ÇÄ, kernel.map]
  simp only [Iso.trans_inv, Iso.app_inv, Iso.symm_inv, Category.assoc, equalizer_as_kernel,
    kernel.lift_Œπ]
  dsimp [asIso]
  simp only [Category.assoc, homology'.œÄ_map, cokernelZeroIsoTarget_hom,
    cokernelIsoOfEq_hom_comp_desc, kernelSubobject_arrow, homology'.œÄ_map_assoc, IsIso.inv_comp_eq]
  simp [homology'.œÄ, kernelSubobjectMap_comp, Iso.refl_hom (X.X 0), Category.comp_id]
#align homological_complex.hom.from_single‚ÇÄ_kernel_at_zero_iso_inv_eq HomologicalComplex.Hom.fromSingle‚ÇÄKernelAtZeroIso_inv_eq

theorem from_single‚ÇÄ_mono_at_zero [hf : QuasiIso' f] : Mono (f.f 0) := by
  constructor
  intro Z g h Hgh
  rw [‚Üê kernel.lift_Œπ (X.d 0 1) (f.f 0) (by rw [f.2 0 1 rfl]; exact zero_comp),
    ‚Üê fromSingle‚ÇÄKernelAtZeroIso_inv_eq] at Hgh
  rw [(@cancel_mono _ _ _ _ _ _ (mono_comp _ _) _ _).1 Hgh]
#align homological_complex.hom.from_single‚ÇÄ_mono_at_zero HomologicalComplex.Hom.from_single‚ÇÄ_mono_at_zero

theorem from_single‚ÇÄ_exact_f_d_at_zero [hf : QuasiIso' f] : Exact (f.f 0) (X.d 0 1) := by
  rw [Preadditive.exact_iff_homology'_zero]
  have h : f.f 0 ‚â´ X.d 0 1 = 0 := by
    simp only [HomologicalComplex.Hom.comm, CochainComplex.single‚ÇÄ_obj_X_d, zero_comp]
  refine' ‚ü®h, Nonempty.intro (homology'IsoCokernelLift _ _ _ ‚â™‚â´ _)‚ü©
  suffices IsIso (kernel.lift (X.d 0 1) (f.f 0) h) by apply cokernel.ofEpi
  rw [‚Üê fromSingle‚ÇÄKernelAtZeroIso_inv_eq f]
  infer_instance
#align homological_complex.hom.from_single‚ÇÄ_exact_f_d_at_zero HomologicalComplex.Hom.from_single‚ÇÄ_exact_f_d_at_zero

theorem from_single‚ÇÄ_exact_at_succ [hf : QuasiIso' f] (n : ‚Ñï) :
    Exact (X.d n (n + 1)) (X.d (n + 1) (n + 2)) :=
  (Preadditive.exact_iff_homology'_zero _ _).2
    ‚ü®X.d_comp_d _ _ _,
      ‚ü®(CochainComplex.homology'SuccIso _ _).symm.trans
          ((@asIso _ _ _ _ _ (hf.1 (n + 1))).symm.trans homology'ZeroZero)‚ü©‚ü©
#align homological_complex.hom.from_single‚ÇÄ_exact_at_succ HomologicalComplex.Hom.from_single‚ÇÄ_exact_at_succ

end

end ToSingle‚ÇÄ

end HomologicalComplex.Hom

variable {A : Type*} [Category A] [Abelian A] {B : Type*} [Category B] [Abelian B] (F : A ‚•§ B)
  [Functor.Additive F] [PreservesFiniteLimits F] [PreservesFiniteColimits F] [Faithful F]

theorem CategoryTheory.Functor.quasiIso'_of_map_quasiIso' {C D : HomologicalComplex A c} (f : C ‚ü∂ D)
    (hf : QuasiIso' ((F.mapHomologicalComplex _).map f)) : QuasiIso' f :=
  ‚ü®fun i =>
    haveI : IsIso (F.map ((homology'Functor A c i).map f)) := by
      rw [‚Üê Functor.comp_map, ‚Üê NatIso.naturality_2 (F.homology'FunctorIso i) f, Functor.comp_map]
      infer_instance
    isIso_of_reflects_iso _ F‚ü©
#align category_theory.functor.quasi_iso_of_map_quasi_iso CategoryTheory.Functor.quasiIso'_of_map_quasiIso'

end-/

section

variable {Œπ : Type _}

variable {V : Type u} [Category.{v} V] [HasZeroMorphisms V]

variable {c : ComplexShape Œπ} {C D E F : HomologicalComplex V c}

class QuasiIsoAt (f : C ‚ü∂ D) (i : Œπ) [C.HasHomology i] [D.HasHomology i] : Prop where
  quasiIso : ShortComplex.QuasiIso ((HomologicalComplex.shortComplexFunctor V c i).map f)

lemma quasiIsoAt_iff (f : C ‚ü∂ D) (i : Œπ) [C.HasHomology i] [D.HasHomology i] :
    QuasiIsoAt f i ‚Üî
      ShortComplex.QuasiIso ((HomologicalComplex.shortComplexFunctor V c i).map f) := by
  constructor
  ¬∑ intro h
    exact h.quasiIso
  ¬∑ intro h
    exact ‚ü®h‚ü©

lemma quasiIsoAt_iff' (f : C ‚ü∂ D) (i j k : Œπ) (hi : c.prev j = i) (hk : c.next j = k)
    [C.HasHomology j] [D.HasHomology j] [(C.sc' i j k).HasHomology] [(D.sc' i j k).HasHomology] :
    QuasiIsoAt f j ‚Üî
      ShortComplex.QuasiIso ((HomologicalComplex.shortComplexFunctor' V c i j k).map f) := by
  rw [quasiIsoAt_iff]
  exact ShortComplex.quasiIso_iff_of_arrow_mk_iso _ _
    (Arrow.isoOfNatIso (HomologicalComplex.natIsoSc' V c i j k hi hk) (Arrow.mk f))

lemma quasiIsoAt_iff_isIso_homologyMap (f : C ‚ü∂ D) (i : Œπ)
    [C.HasHomology i] [D.HasHomology i] :
    QuasiIsoAt f i ‚Üî IsIso ((HomologicalComplex.homologyMap f i)) := by
  rw [quasiIsoAt_iff, ShortComplex.quasiIso_iff]
  rfl

lemma quasiIsoAt_iff_exactAt (f : C ‚ü∂ D) (i : Œπ) [C.HasHomology i] [D.HasHomology i]
    (hC : C.ExactAt i) :
    QuasiIsoAt f i ‚Üî D.ExactAt i := by
  simp only [quasiIsoAt_iff, ShortComplex.quasiIso_iff, HomologicalComplex.exactAt_iff,
    ShortComplex.exact_iff_isZero_homology] at hC ‚ä¢
  constructor
  ¬∑ intro h
    exact IsZero.of_iso hC (@asIso _ _ _ _ _ h).symm
  ¬∑ intro hD
    exact ‚ü®‚ü®0, IsZero.eq_of_src hC _ _, IsZero.eq_of_tgt hD _ _‚ü©‚ü©

lemma quasiIsoAt_iff_exactAt' (f : C ‚ü∂ D) (i : Œπ) [C.HasHomology i] [D.HasHomology i]
    (hD : D.ExactAt i) :
    QuasiIsoAt f i ‚Üî C.ExactAt i := by
  simp only [quasiIsoAt_iff, ShortComplex.quasiIso_iff, HomologicalComplex.exactAt_iff,
    ShortComplex.exact_iff_isZero_homology] at hD ‚ä¢
  constructor
  ¬∑ intro h
    exact IsZero.of_iso hD (@asIso _ _ _ _ _ h)
  ¬∑ intro hC
    exact ‚ü®‚ü®0, IsZero.eq_of_src hC _ _, IsZero.eq_of_tgt hD _ _‚ü©‚ü©

instance (f : C ‚ü∂ D) (i : Œπ) [C.HasHomology i] [D.HasHomology i] [hf : QuasiIsoAt f i] :
    IsIso (HomologicalComplex.homologyMap f i) := by
  simpa only [quasiIsoAt_iff, ShortComplex.quasiIso_iff] using hf

@[simps! hom]
noncomputable def isoOfQuasiIsoAt (f : C ‚ü∂ D) (i : Œπ) [C.HasHomology i] [D.HasHomology i]
    [QuasiIsoAt f i] : C.homology i ‚âÖ D.homology i :=
  asIso (HomologicalComplex.homologyMap f i)

@[reassoc (attr := simp)]
lemma isoOfQuasiIsoAt_hom_inv_id (f : C ‚ü∂ D) (i : Œπ) [C.HasHomology i] [D.HasHomology i]
    [QuasiIsoAt f i] :
    HomologicalComplex.homologyMap f i ‚â´ (isoOfQuasiIsoAt f i).inv = ùüô _ :=
  (isoOfQuasiIsoAt f i).hom_inv_id

@[reassoc (attr := simp)]
lemma isoOfQuasiIsoAt_inv_hom_id (f : C ‚ü∂ D) (i : Œπ) [C.HasHomology i] [D.HasHomology i]
    [QuasiIsoAt f i] :
    (isoOfQuasiIsoAt f i).inv ‚â´ HomologicalComplex.homologyMap f i = ùüô _ :=
  (isoOfQuasiIsoAt f i).inv_hom_id

class QuasiIso (f : C ‚ü∂ D) [‚àÄ i, C.HasHomology i] [‚àÄ i, D.HasHomology i] : Prop where
  quasiIso : ‚àÄ i, QuasiIsoAt f i := by infer_instance

attribute [instance] QuasiIso.quasiIso

instance quasiIsoAt_of_isIso (f : C ‚ü∂ D) [IsIso f] (i : Œπ) [C.HasHomology i] [D.HasHomology i] :
    QuasiIsoAt f i := by
  rw [quasiIsoAt_iff]
  infer_instance

instance quasiIso_of_isIso (f : C ‚ü∂ D) [IsIso f] [‚àÄ i, C.HasHomology i] [‚àÄ i, D.HasHomology i] :
    QuasiIso f where

lemma quasiIso_iff_mem_qis (f : C ‚ü∂ D) [CategoryWithHomology V] :
    QuasiIso f ‚Üî HomologicalComplex.qis _ _ f := by
  dsimp [HomologicalComplex.qis]
  simp only [‚Üê quasiIsoAt_iff_isIso_homologyMap f]
  constructor
  ¬∑ intro h i
    infer_instance
  ¬∑ intro h
    exact ‚ü®h‚ü©

lemma CochainComplex.quasiIsoAt‚ÇÄ_iff {K L : CochainComplex V ‚Ñï} (f : K ‚ü∂ L)
    [K.HasHomology 0] [L.HasHomology 0] [(K.sc' 0 0 1).HasHomology] [(L.sc' 0 0 1).HasHomology] :
    QuasiIsoAt f 0 ‚Üî
      ShortComplex.QuasiIso ((HomologicalComplex.shortComplexFunctor' V _ 0 0 1).map f) :=
  quasiIsoAt_iff' _ _ _ _ (by simp) (by simp)

lemma ChainComplex.quasiIsoAt‚ÇÄ_iff {K L : ChainComplex V ‚Ñï} (f : K ‚ü∂ L)
    [K.HasHomology 0] [L.HasHomology 0] [(K.sc' 1 0 0).HasHomology] [(L.sc' 1 0 0).HasHomology] :
    QuasiIsoAt f 0 ‚Üî
      ShortComplex.QuasiIso ((HomologicalComplex.shortComplexFunctor' V _ 1 0 0).map f) :=
  quasiIsoAt_iff' _ _ _ _ (by simp) (by simp)

lemma quasiIsoAt_comp'' (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E) (n : Œπ) [C.HasHomology n]
    [D.HasHomology n] [E.HasHomology n]
    (hœÜ : QuasiIsoAt œÜ n) (hœÜ' : QuasiIsoAt œÜ' n) :
    QuasiIsoAt (œÜ ‚â´ œÜ') n := by
  rw [quasiIsoAt_iff] at hœÜ hœÜ' ‚ä¢
  rw [Functor.map_comp]
  exact ShortComplex.quasiIso_comp _ _

instance quasiIsoAt_comp (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E) (n : Œπ)
    [C.HasHomology n] [D.HasHomology n] [E.HasHomology n]
    [hœÜ : QuasiIsoAt œÜ n] [hœÜ' : QuasiIsoAt œÜ' n] :
    QuasiIsoAt (œÜ ‚â´ œÜ') n :=
  quasiIsoAt_comp'' œÜ œÜ' n hœÜ hœÜ'

lemma quasiIso_comp'' (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E)
    [‚àÄ n, C.HasHomology n] [‚àÄ n, D.HasHomology n] [‚àÄ n, E.HasHomology n]
    (_ : QuasiIso œÜ) (_ : QuasiIso œÜ') :
    QuasiIso (œÜ ‚â´ œÜ') where
  quasiIso n := quasiIsoAt_comp œÜ œÜ' n

instance quasiIso_comp (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E)
    [‚àÄ n, C.HasHomology n] [‚àÄ n, D.HasHomology n] [‚àÄ n, E.HasHomology n]
    [hœÜ : QuasiIso œÜ] [hœÜ' : QuasiIso œÜ'] :
    QuasiIso (œÜ ‚â´ œÜ') :=
  quasiIso_comp'' œÜ œÜ' hœÜ hœÜ'

lemma quasiIsoAt_of_comp_left'' (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E) (n : Œπ)
    [C.HasHomology n] [D.HasHomology n] [E.HasHomology n]
    (hœÜ : QuasiIsoAt œÜ n) (hœÜœÜ' : QuasiIsoAt (œÜ ‚â´ œÜ') n) :
    QuasiIsoAt œÜ' n := by
  rw [quasiIsoAt_iff] at hœÜ hœÜœÜ' ‚ä¢
  rw [Functor.map_comp] at hœÜœÜ'
  exact ShortComplex.quasiIso_of_comp_left (hœÜ := hœÜ) (hœÜœÜ' := hœÜœÜ')

lemma quasiIsoAt_of_comp_left (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E) (n : Œπ)
    [C.HasHomology n] [D.HasHomology n] [E.HasHomology n]
    [hœÜ : QuasiIsoAt œÜ n] [hœÜœÜ' : QuasiIsoAt (œÜ ‚â´ œÜ') n] :
    QuasiIsoAt œÜ' n :=
  quasiIsoAt_of_comp_left'' œÜ œÜ' n hœÜ hœÜœÜ'

lemma quasiIso_of_comp_left'' (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E)
    [‚àÄ n, C.HasHomology n] [‚àÄ n, D.HasHomology n] [‚àÄ n, E.HasHomology n]
    (_ : QuasiIso œÜ) (_ : QuasiIso (œÜ ‚â´ œÜ')) :
    QuasiIso œÜ' where
  quasiIso n := quasiIsoAt_of_comp_left œÜ œÜ' n

lemma quasiIso_of_comp_left (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E)
    [‚àÄ n, C.HasHomology n] [‚àÄ n, D.HasHomology n] [‚àÄ n, E.HasHomology n]
    [hœÜ : QuasiIso œÜ] [hœÜœÜ' : QuasiIso (œÜ ‚â´ œÜ')] :
    QuasiIso œÜ' :=
  quasiIso_of_comp_left'' œÜ œÜ' hœÜ hœÜœÜ'

@[simp]
lemma quasiIsoAt_iff_comp_left'' (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E) (n : Œπ)
    [C.HasHomology n] [D.HasHomology n] [E.HasHomology n]
    (hœÜ : QuasiIsoAt œÜ n) :
    QuasiIsoAt (œÜ ‚â´ œÜ') n ‚Üî QuasiIsoAt œÜ' n := by
  constructor
  ¬∑ intro hœÜœÜ'
    exact quasiIsoAt_of_comp_left œÜ œÜ' n
  ¬∑ intro hœÜ
    infer_instance

@[simp]
lemma quasiIsoAt_iff_comp_left (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E) (n : Œπ)
    [C.HasHomology n] [D.HasHomology n] [E.HasHomology n]
    [hœÜ : QuasiIsoAt œÜ n] :
    QuasiIsoAt (œÜ ‚â´ œÜ') n ‚Üî QuasiIsoAt œÜ' n :=
  quasiIsoAt_iff_comp_left'' œÜ œÜ' n hœÜ

@[simp]
lemma quasiIso_iff_comp_left'' (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E)
    [‚àÄ n, C.HasHomology n] [‚àÄ n, D.HasHomology n] [‚àÄ n, E.HasHomology n]
    (hœÜ : QuasiIso œÜ) :
    QuasiIso (œÜ ‚â´ œÜ') ‚Üî QuasiIso œÜ' := by
  constructor
  ¬∑ intro
    exact quasiIso_of_comp_left œÜ œÜ'
  ¬∑ intro hœÜ'
    infer_instance

@[simp]
lemma quasiIso_iff_comp_left (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E)
    [‚àÄ n, C.HasHomology n] [‚àÄ n, D.HasHomology n] [‚àÄ n, E.HasHomology n]
    [hœÜ : QuasiIso œÜ] :
    QuasiIso (œÜ ‚â´ œÜ') ‚Üî QuasiIso œÜ' :=
  quasiIso_iff_comp_left'' œÜ œÜ' hœÜ

-----

lemma quasiIsoAt_of_comp_right'' (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E) (n : Œπ)
    [C.HasHomology n] [D.HasHomology n] [E.HasHomology n]
    (hœÜ' : QuasiIsoAt œÜ' n) (hœÜœÜ' : QuasiIsoAt (œÜ ‚â´ œÜ') n) :
    QuasiIsoAt œÜ n := by
  rw [quasiIsoAt_iff] at hœÜ' hœÜœÜ' ‚ä¢
  rw [Functor.map_comp] at hœÜœÜ'
  exact ShortComplex.quasiIso_of_comp_right (hœÜ' := hœÜ') (hœÜœÜ' := hœÜœÜ')

lemma quasiIsoAt_of_comp_right (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E) (n : Œπ)
    [C.HasHomology n] [D.HasHomology n] [E.HasHomology n]
    [hœÜ' : QuasiIsoAt œÜ' n] [hœÜœÜ' : QuasiIsoAt (œÜ ‚â´ œÜ') n] :
    QuasiIsoAt œÜ n :=
  quasiIsoAt_of_comp_right'' œÜ œÜ' n hœÜ' hœÜœÜ'

lemma quasiIso_of_comp_right'' (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E)
    [‚àÄ n, C.HasHomology n] [‚àÄ n, D.HasHomology n] [‚àÄ n, E.HasHomology n]
    (_ : QuasiIso œÜ') (_ : QuasiIso (œÜ ‚â´ œÜ')) :
    QuasiIso œÜ where
  quasiIso n := quasiIsoAt_of_comp_right œÜ œÜ' n

lemma quasiIso_of_comp_right (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E)
    [‚àÄ n, C.HasHomology n] [‚àÄ n, D.HasHomology n] [‚àÄ n, E.HasHomology n]
    [hœÜ' : QuasiIso œÜ'] [hœÜœÜ' : QuasiIso (œÜ ‚â´ œÜ')] :
    QuasiIso œÜ :=
  quasiIso_of_comp_right'' œÜ œÜ' hœÜ' hœÜœÜ'

@[simp]
lemma quasiIsoAt_iff_comp_right'' (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E) (n : Œπ)
    [C.HasHomology n] [D.HasHomology n] [E.HasHomology n]
    (hœÜ' : QuasiIsoAt œÜ' n) :
    QuasiIsoAt (œÜ ‚â´ œÜ') n ‚Üî QuasiIsoAt œÜ n := by
  constructor
  ¬∑ intro hœÜœÜ'
    exact quasiIsoAt_of_comp_right œÜ œÜ' n
  ¬∑ intro hœÜ
    infer_instance

@[simp]
lemma quasiIsoAt_iff_comp_right (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E) (n : Œπ)
    [C.HasHomology n] [D.HasHomology n] [E.HasHomology n]
    [hœÜ' : QuasiIsoAt œÜ' n] :
    QuasiIsoAt (œÜ ‚â´ œÜ') n ‚Üî QuasiIsoAt œÜ n :=
  quasiIsoAt_iff_comp_right'' œÜ œÜ' n hœÜ'

@[simp]
lemma quasiIso_iff_comp_right'' (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E)
    [‚àÄ n, C.HasHomology n] [‚àÄ n, D.HasHomology n] [‚àÄ n, E.HasHomology n]
    (hœÜ' : QuasiIso œÜ') :
    QuasiIso (œÜ ‚â´ œÜ') ‚Üî QuasiIso œÜ := by
  constructor
  ¬∑ intro
    exact quasiIso_of_comp_right œÜ œÜ'
  ¬∑ intro hœÜ
    infer_instance

@[simp]
lemma quasiIso_iff_comp_right (œÜ : C ‚ü∂ D) (œÜ' : D ‚ü∂ E)
    [‚àÄ n, C.HasHomology n] [‚àÄ n, D.HasHomology n] [‚àÄ n, E.HasHomology n]
    [hœÜ' : QuasiIso œÜ'] :
    QuasiIso (œÜ ‚â´ œÜ') ‚Üî QuasiIso œÜ :=
  quasiIso_iff_comp_right'' œÜ œÜ' hœÜ'

lemma quasiIsoAt_of_arrow_mk_iso'
    (œÜ : C ‚ü∂ D) (œÜ' : E ‚ü∂ F) (e : Arrow.mk œÜ ‚âÖ Arrow.mk œÜ') (n : Œπ)
    [C.HasHomology n] [D.HasHomology n] [E.HasHomology n] [F.HasHomology n]
    (hœÜ : QuasiIsoAt œÜ n) : QuasiIsoAt œÜ' n := by
  let Œ± : E ‚ü∂ C := e.inv.left
  let Œ≤ : D ‚ü∂ F := e.hom.right
  suffices œÜ' = Œ± ‚â´ œÜ ‚â´ Œ≤ by
    rw [this]
    infer_instance
  simp only [Arrow.w_mk_right_assoc, Arrow.mk_left, Arrow.mk_right, Arrow.mk_hom,
    ‚Üê Arrow.comp_right, e.inv_hom_id, Arrow.id_right, Category.comp_id]

lemma quasiIsoAt_of_arrow_mk_iso
    (œÜ : C ‚ü∂ D) (œÜ' : E ‚ü∂ F) (e : Arrow.mk œÜ ‚âÖ Arrow.mk œÜ') (n : Œπ)
    [C.HasHomology n] [D.HasHomology n] [E.HasHomology n] [F.HasHomology n]
    [QuasiIsoAt œÜ n] : QuasiIsoAt œÜ' n :=
  quasiIsoAt_of_arrow_mk_iso' œÜ œÜ' e n inferInstance

lemma quasiIsoAt_iff_of_arrow_mk_iso
    (œÜ : C ‚ü∂ D) (œÜ' : E ‚ü∂ F) (e : Arrow.mk œÜ ‚âÖ Arrow.mk œÜ') (n : Œπ)
    [C.HasHomology n] [D.HasHomology n] [E.HasHomology n] [F.HasHomology n] :
    QuasiIsoAt œÜ n ‚Üî QuasiIsoAt œÜ' n :=
  ‚ü®quasiIsoAt_of_arrow_mk_iso' œÜ œÜ' e n, quasiIsoAt_of_arrow_mk_iso' œÜ' œÜ e.symm n‚ü©

lemma quasiIso_of_arrow_mk_iso'
    (œÜ : C ‚ü∂ D) (œÜ' : E ‚ü∂ F) (e : Arrow.mk œÜ ‚âÖ Arrow.mk œÜ')
    [‚àÄ n, C.HasHomology n] [‚àÄ n, D.HasHomology n] [‚àÄ n, E.HasHomology n] [‚àÄ n, F.HasHomology n]
    (_ : QuasiIso œÜ) : QuasiIso œÜ' where
  quasiIso n := quasiIsoAt_of_arrow_mk_iso œÜ œÜ' e n

lemma quasiIso_of_arrow_mk_iso
    (œÜ : C ‚ü∂ D) (œÜ' : E ‚ü∂ F) (e : Arrow.mk œÜ ‚âÖ Arrow.mk œÜ')
    [‚àÄ n, C.HasHomology n] [‚àÄ n, D.HasHomology n] [‚àÄ n, E.HasHomology n] [‚àÄ n, F.HasHomology n]
    [QuasiIso œÜ] : QuasiIso œÜ' :=
  quasiIso_of_arrow_mk_iso' œÜ œÜ' e inferInstance

lemma quasiIso_iff_of_arrow_mk_iso
    (œÜ : C ‚ü∂ D) (œÜ' : E ‚ü∂ F) (e : Arrow.mk œÜ ‚âÖ Arrow.mk œÜ')
    [‚àÄ n, C.HasHomology n] [‚àÄ n, D.HasHomology n] [‚àÄ n, E.HasHomology n] [‚àÄ n, F.HasHomology n] :
    QuasiIso œÜ ‚Üî QuasiIso œÜ' :=
  ‚ü®quasiIso_of_arrow_mk_iso' œÜ œÜ' e, quasiIso_of_arrow_mk_iso' œÜ' œÜ e.symm‚ü©

end

section

variable {C D : Type _} [Category C] [Preadditive C]
  [Category D] [Preadditive D] (F : C ‚•§ D) [F.Additive]
  {Œπ : Type _} {c : ComplexShape Œπ} {K L : HomologicalComplex C c} (f : K ‚ü∂ L)

instance CategoryTheory.Functor.map_quasiIsoAt_of_preservesHomology
    [F.PreservesHomology] (n : Œπ)
    [K.HasHomology n] [L.HasHomology n]
    [((F.mapHomologicalComplex c).obj K).HasHomology n]
    [((F.mapHomologicalComplex c).obj L).HasHomology n]
    [hf : QuasiIsoAt f n] : QuasiIsoAt ((F.mapHomologicalComplex c).map f) n := by
  rw [quasiIsoAt_iff] at hf ‚ä¢
  exact ShortComplex.quasiIso_map_of_preservesRightHomology F ((HomologicalComplex.shortComplexFunctor C c n).map f)

instance CategoryTheory.Functor.map_quasiIso_of_preservesHomology
    [F.PreservesHomology] [‚àÄ n, K.HasHomology n] [‚àÄ n, L.HasHomology n]
    [‚àÄ n, ((F.mapHomologicalComplex c).obj K).HasHomology n]
    [‚àÄ n, ((F.mapHomologicalComplex c).obj L).HasHomology n]
    [QuasiIso f] : QuasiIso ((F.mapHomologicalComplex c).map f) where

lemma CategoryTheory.Functor.quasiIsoAt_of_map_quasiIsoAt_of_preservesHomology
    [F.PreservesHomology] [ReflectsIsomorphisms F] (n : Œπ)
    [K.HasHomology n] [L.HasHomology n]
    [((F.mapHomologicalComplex c).obj K).HasHomology n]
    [((F.mapHomologicalComplex c).obj L).HasHomology n]
    (hf : QuasiIsoAt ((F.mapHomologicalComplex c).map f) n) :
    QuasiIsoAt f n := by
  rw [quasiIsoAt_iff] at hf ‚ä¢
  exact (ShortComplex.quasiIso_map_iff_of_preservesRightHomology
    F ((HomologicalComplex.shortComplexFunctor C c n).map f)).1  hf

lemma CategoryTheory.Functor.quasiIso_of_map_quasiIso_of_preservesHomology
    [F.PreservesHomology] [ReflectsIsomorphisms F] [‚àÄ n, K.HasHomology n] [‚àÄ n, L.HasHomology n]
    [‚àÄ n, ((F.mapHomologicalComplex c).obj K).HasHomology n]
    [‚àÄ n, ((F.mapHomologicalComplex c).obj L).HasHomology n]
    (_ : QuasiIso ((F.mapHomologicalComplex c).map f)) :
    QuasiIso f where
  quasiIso n := F.quasiIsoAt_of_map_quasiIsoAt_of_preservesHomology f n inferInstance

end

section

variable {A : Type _} [Category A] [Preadditive A] {Œπ : Type _} {c : ComplexShape Œπ}
  {K L : HomologicalComplex A c} (e : HomotopyEquiv K L) [DecidableRel c.Rel]

instance HomotopyEquiv.toQuasiIsoAt (n : Œπ) [K.HasHomology n] [L.HasHomology n] :
    QuasiIsoAt e.hom n := by
  rw [quasiIsoAt_iff, ShortComplex.quasiIso_iff]
  exact IsIso.of_iso (e.toHomologyIso n)

def HomotopyEquiv.toQuasiIso [‚àÄ n, K.HasHomology n] [‚àÄ n, L.HasHomology n] :
    QuasiIso e.hom :=
  ‚ü®fun _ => inferInstance‚ü©

end
