/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.Homotopy
import Mathlib.Algebra.Homology.SingleHomology
import Mathlib.CategoryTheory.Abelian.Homology

#align_import algebra.homology.quasi_iso from "leanprover-community/mathlib"@"956af7c76589f444f2e1313911bad16366ea476d"

/-!
# Quasi-isomorphisms

A chain map is a quasi-isomorphism if it induces isomorphisms on homology.

## Future work

Define the derived category as the localization at quasi-isomorphisms? (TODO @joelriou)
-/


open CategoryTheory Limits

universe v u

variable {Î¹ : Type*}

section

variable {V : Type u} [Category.{v} V] [HasZeroMorphisms V] [HasZeroObject V]

variable [HasEqualizers V] [HasImages V] [HasImageMaps V] [HasCokernels V]

variable {c : ComplexShape Î¹} {C D E : HomologicalComplex V c}

/-- A chain map is a quasi-isomorphism if it induces isomorphisms on homology.
-/
class QuasiIso' (f : C âŸ¶ D) : Prop where
  isIso : âˆ€ i, IsIso ((homology'Functor V c i).map f)
#align quasi_iso QuasiIso'

attribute [instance] QuasiIso'.isIso

instance (priority := 100) quasiIso'_of_iso (f : C âŸ¶ D) [IsIso f] : QuasiIso' f where
  isIso i := by
    change IsIso ((homology'Functor V c i).mapIso (asIso f)).hom
    infer_instance
#align quasi_iso_of_iso quasiIso'_of_iso

instance quasiIso'_comp (f : C âŸ¶ D) [QuasiIso' f] (g : D âŸ¶ E) [QuasiIso' g] :
    QuasiIso' (f â‰« g) where
  isIso i := by
    rw [Functor.map_comp]
    infer_instance
#align quasi_iso_comp quasiIso'_comp

theorem quasiIso'_of_comp_left (f : C âŸ¶ D) [QuasiIso' f] (g : D âŸ¶ E) [QuasiIso' (f â‰« g)] :
    QuasiIso' g :=
  { isIso := fun i => IsIso.of_isIso_fac_left ((homology'Functor V c i).map_comp f g).symm }
#align quasi_iso_of_comp_left quasiIso'_of_comp_left

theorem quasiIso'_of_comp_right (f : C âŸ¶ D) (g : D âŸ¶ E) [QuasiIso' g] [QuasiIso' (f â‰« g)] :
    QuasiIso' f :=
  { isIso := fun i => IsIso.of_isIso_fac_right ((homology'Functor V c i).map_comp f g).symm }
#align quasi_iso_of_comp_right quasiIso'_of_comp_right

namespace HomotopyEquiv

section

variable {W : Type*} [Category W] [Preadditive W] [HasCokernels W] [HasImages W] [HasEqualizers W]
  [HasZeroObject W] [HasImageMaps W]

/-- A homotopy equivalence is a quasi-isomorphism. -/
theorem toQuasiIso' {C D : HomologicalComplex W c} (e : HomotopyEquiv C D) : QuasiIso' e.hom :=
  âŸ¨fun i => by
    refine' âŸ¨âŸ¨(homology'Functor W c i).map e.inv, _âŸ©âŸ©
    simp only [â† Functor.map_comp, â† (homology'Functor W c i).map_id]
    constructor <;> apply homology'_map_eq_of_homotopy
    exacts [e.homotopyHomInvId, e.homotopyInvHomId]âŸ©
#align homotopy_equiv.to_quasi_iso HomotopyEquiv.toQuasiIso'

theorem toQuasiIso'_inv {C D : HomologicalComplex W c} (e : HomotopyEquiv C D) (i : Î¹) :
    (@asIso _ _ _ _ _ (e.toQuasiIso'.1 i)).inv = (homology'Functor W c i).map e.inv := by
  symm
  haveI := e.toQuasiIso'.1 i -- Porting note: Added this to get `asIso_hom` to work.
  simp only [â† Iso.hom_comp_eq_id, asIso_hom, â† Functor.map_comp,
    â† (homology'Functor W c i).map_id, homology'_map_eq_of_homotopy e.homotopyHomInvId _]
#align homotopy_equiv.to_quasi_iso_inv HomotopyEquiv.toQuasiIso'_inv

end

end HomotopyEquiv

namespace HomologicalComplex.Hom

section ToSingleâ‚€

variable {W : Type*} [Category W] [Abelian W]

section

variable {X : ChainComplex W â„•} {Y : W} (f : X âŸ¶ (ChainComplex.singleâ‚€ _).obj Y) [hf : QuasiIso' f]

/-- If a chain map `f : X âŸ¶ Y[0]` is a quasi-isomorphism, then the cokernel of the differential
`d : Xâ‚ â†’ Xâ‚€` is isomorphic to `Y`. -/
noncomputable def toSingleâ‚€CokernelAtZeroIso : cokernel (X.d 1 0) â‰… Y :=
  X.homology'ZeroIso.symm.trans
    ((@asIso _ _ _ _ _ (hf.1 0)).trans ((ChainComplex.homology'Functor0Singleâ‚€ W).app Y))
#align homological_complex.hom.to_singleâ‚€_cokernel_at_zero_iso HomologicalComplex.Hom.toSingleâ‚€CokernelAtZeroIso

theorem toSingleâ‚€CokernelAtZeroIso_hom_eq [hf : QuasiIso' f] :
    f.toSingleâ‚€CokernelAtZeroIso.hom =
      cokernel.desc (X.d 1 0) (f.f 0) (by rw [â† f.2 1 0 rfl]; exact comp_zero) := by
  ext
  dsimp only [toSingleâ‚€CokernelAtZeroIso, ChainComplex.homology'ZeroIso, homology'OfZeroRight,
    homology'.mapIso, ChainComplex.homology'Functor0Singleâ‚€, cokernel.map]
  dsimp [asIso]
  simp only [cokernel.Ï€_desc, Category.assoc, homology'.map_desc, cokernel.Ï€_desc_assoc]
  simp [homology'.desc, Iso.refl_inv (X.X 0)]
#align homological_complex.hom.to_singleâ‚€_cokernel_at_zero_iso_hom_eq HomologicalComplex.Hom.toSingleâ‚€CokernelAtZeroIso_hom_eq

theorem to_singleâ‚€_epi_at_zero [hf : QuasiIso' f] : Epi (f.f 0) := by
  constructor
  intro Z g h Hgh
  rw [â† cokernel.Ï€_desc (X.d 1 0) (f.f 0) (by rw [â† f.2 1 0 rfl]; exact comp_zero),
    â† toSingleâ‚€CokernelAtZeroIso_hom_eq] at Hgh
  rw [(@cancel_epi _ _ _ _ _ _ (epi_comp _ _) _ _).1 Hgh]
#align homological_complex.hom.to_singleâ‚€_epi_at_zero HomologicalComplex.Hom.to_singleâ‚€_epi_at_zero

theorem to_singleâ‚€_exact_d_f_at_zero [hf : QuasiIso' f] : Exact (X.d 1 0) (f.f 0) := by
  rw [Preadditive.exact_iff_homology'_zero]
  have h : X.d 1 0 â‰« f.f 0 = 0 := by simp only [â† f.comm 1 0, single_obj_d, comp_zero]
  refine' âŸ¨h, Nonempty.intro (homology'IsoKernelDesc _ _ _ â‰ªâ‰« _)âŸ©
  suffices IsIso (cokernel.desc _ _ h) by apply kernel.ofMono
  rw [â† toSingleâ‚€CokernelAtZeroIso_hom_eq]
  infer_instance
#align homological_complex.hom.to_singleâ‚€_exact_d_f_at_zero HomologicalComplex.Hom.to_singleâ‚€_exact_d_f_at_zero

theorem to_singleâ‚€_exact_at_succ [hf : QuasiIso' f] (n : â„•) :
    Exact (X.d (n + 2) (n + 1)) (X.d (n + 1) n) :=
  (Preadditive.exact_iff_homology'_zero _ _).2
    âŸ¨X.d_comp_d _ _ _,
      âŸ¨(ChainComplex.homology'SuccIso _ _).symm.trans
          ((@asIso _ _ _ _ _ (hf.1 (n + 1))).trans homology'ZeroZero)âŸ©âŸ©
#align homological_complex.hom.to_singleâ‚€_exact_at_succ HomologicalComplex.Hom.to_singleâ‚€_exact_at_succ

end

section

variable {X : CochainComplex W â„•} {Y : W} (f : (CochainComplex.singleâ‚€ _).obj Y âŸ¶ X)

/-- If a cochain map `f : Y[0] âŸ¶ X` is a quasi-isomorphism, then the kernel of the differential
`d : Xâ‚€ â†’ Xâ‚` is isomorphic to `Y`. -/
noncomputable def fromSingleâ‚€KernelAtZeroIso [hf : QuasiIso' f] : kernel (X.d 0 1) â‰… Y :=
  X.homology'ZeroIso.symm.trans
    ((@asIso _ _ _ _ _ (hf.1 0)).symm.trans ((CochainComplex.homologyFunctor0Singleâ‚€ W).app Y))
#align homological_complex.hom.from_singleâ‚€_kernel_at_zero_iso HomologicalComplex.Hom.fromSingleâ‚€KernelAtZeroIso

theorem fromSingleâ‚€KernelAtZeroIso_inv_eq [hf : QuasiIso' f] :
    f.fromSingleâ‚€KernelAtZeroIso.inv =
      kernel.lift (X.d 0 1) (f.f 0) (by rw [f.2 0 1 rfl]; exact zero_comp) := by
  ext
  dsimp only [fromSingleâ‚€KernelAtZeroIso, CochainComplex.homology'ZeroIso, homology'OfZeroLeft,
    homology'.mapIso, CochainComplex.homologyFunctor0Singleâ‚€, kernel.map]
  simp only [Iso.trans_inv, Iso.app_inv, Iso.symm_inv, Category.assoc, equalizer_as_kernel,
    kernel.lift_Î¹]
  dsimp [asIso]
  simp only [Category.assoc, homology'.Ï€_map, cokernelZeroIsoTarget_hom,
    cokernelIsoOfEq_hom_comp_desc, kernelSubobject_arrow, homology'.Ï€_map_assoc, IsIso.inv_comp_eq]
  simp [homology'.Ï€, kernelSubobjectMap_comp, Iso.refl_hom (X.X 0), Category.comp_id]
#align homological_complex.hom.from_singleâ‚€_kernel_at_zero_iso_inv_eq HomologicalComplex.Hom.fromSingleâ‚€KernelAtZeroIso_inv_eq

theorem from_singleâ‚€_mono_at_zero [hf : QuasiIso' f] : Mono (f.f 0) := by
  constructor
  intro Z g h Hgh
  rw [â† kernel.lift_Î¹ (X.d 0 1) (f.f 0) (by rw [f.2 0 1 rfl]; exact zero_comp),
    â† fromSingleâ‚€KernelAtZeroIso_inv_eq] at Hgh
  rw [(@cancel_mono _ _ _ _ _ _ (mono_comp _ _) _ _).1 Hgh]
#align homological_complex.hom.from_singleâ‚€_mono_at_zero HomologicalComplex.Hom.from_singleâ‚€_mono_at_zero

theorem from_singleâ‚€_exact_f_d_at_zero [hf : QuasiIso' f] : Exact (f.f 0) (X.d 0 1) := by
  rw [Preadditive.exact_iff_homology'_zero]
  have h : f.f 0 â‰« X.d 0 1 = 0 := by simp
  refine' âŸ¨h, Nonempty.intro (homology'IsoCokernelLift _ _ _ â‰ªâ‰« _)âŸ©
  suffices IsIso (kernel.lift (X.d 0 1) (f.f 0) h) by apply cokernel.ofEpi
  rw [â† fromSingleâ‚€KernelAtZeroIso_inv_eq f]
  infer_instance
#align homological_complex.hom.from_singleâ‚€_exact_f_d_at_zero HomologicalComplex.Hom.from_singleâ‚€_exact_f_d_at_zero

theorem from_singleâ‚€_exact_at_succ [hf : QuasiIso' f] (n : â„•) :
    Exact (X.d n (n + 1)) (X.d (n + 1) (n + 2)) :=
  (Preadditive.exact_iff_homology'_zero _ _).2
    âŸ¨X.d_comp_d _ _ _,
      âŸ¨(CochainComplex.homology'SuccIso _ _).symm.trans
          ((@asIso _ _ _ _ _ (hf.1 (n + 1))).symm.trans homology'ZeroZero)âŸ©âŸ©
#align homological_complex.hom.from_singleâ‚€_exact_at_succ HomologicalComplex.Hom.from_singleâ‚€_exact_at_succ

end

end ToSingleâ‚€

end HomologicalComplex.Hom

variable {A : Type*} [Category A] [Abelian A] {B : Type*} [Category B] [Abelian B] (F : A â¥¤ B)
  [Functor.Additive F] [PreservesFiniteLimits F] [PreservesFiniteColimits F] [Faithful F]

theorem CategoryTheory.Functor.quasiIso'_of_map_quasiIso' {C D : HomologicalComplex A c}
    (f : C âŸ¶ D) (hf : QuasiIso' ((F.mapHomologicalComplex _).map f)) : QuasiIso' f :=
  âŸ¨fun i =>
    haveI : IsIso (F.map ((homology'Functor A c i).map f)) := by
      rw [â† Functor.comp_map, â† NatIso.naturality_2 (F.homology'FunctorIso i) f, Functor.comp_map]
      infer_instance
    isIso_of_reflects_iso _ FâŸ©
#align category_theory.functor.quasi_iso_of_map_quasi_iso CategoryTheory.Functor.quasiIso'_of_map_quasiIso'

end

open HomologicalComplex

variable {Î¹ : Type*} {C : Type u} [Category.{v} C] [HasZeroMorphisms C]
  {c : ComplexShape Î¹} {K L M K' L' : HomologicalComplex C c}

/-- A morphism of homological complexes `f : K âŸ¶ L` is a quasi-isomorphism in degree `i`
when it induces a quasi-isomorphism of short complexes `K.sc i âŸ¶ L.sc i`. -/
class QuasiIsoAt (f : K âŸ¶ L) (i : Î¹) [K.HasHomology i] [L.HasHomology i] : Prop where
  quasiIso : ShortComplex.QuasiIso ((shortComplexFunctor C c i).map f)

lemma quasiIsoAt_iff (f : K âŸ¶ L) (i : Î¹) [K.HasHomology i] [L.HasHomology i] :
    QuasiIsoAt f i â†”
      ShortComplex.QuasiIso ((shortComplexFunctor C c i).map f) := by
  constructor
  Â· intro h
    exact h.quasiIso
  Â· intro h
    exact âŸ¨hâŸ©

instance quasiIsoAt_of_isIso (f : K âŸ¶ L) [IsIso f] (i : Î¹) [K.HasHomology i] [L.HasHomology i] :
    QuasiIsoAt f i := by
  rw [quasiIsoAt_iff]
  infer_instance

lemma quasiIsoAt_iff' (f : K âŸ¶ L) (i j k : Î¹) (hi : c.prev j = i) (hk : c.next j = k)
    [K.HasHomology j] [L.HasHomology j] [(K.sc' i j k).HasHomology] [(L.sc' i j k).HasHomology] :
    QuasiIsoAt f j â†”
      ShortComplex.QuasiIso ((shortComplexFunctor' C c i j k).map f) := by
  rw [quasiIsoAt_iff]
  exact ShortComplex.quasiIso_iff_of_arrow_mk_iso _ _
    (Arrow.isoOfNatIso (natIsoSc' C c i j k hi hk) (Arrow.mk f))

lemma quasiIsoAt_iff_isIso_homologyMap (f : K âŸ¶ L) (i : Î¹)
    [K.HasHomology i] [L.HasHomology i] :
    QuasiIsoAt f i â†” IsIso (homologyMap f i) := by
  rw [quasiIsoAt_iff, ShortComplex.quasiIso_iff]
  rfl

lemma quasiIsoAt_iff_exactAt (f : K âŸ¶ L) (i : Î¹) [K.HasHomology i] [L.HasHomology i]
    (hK : K.ExactAt i) :
    QuasiIsoAt f i â†” L.ExactAt i := by
  simp only [quasiIsoAt_iff, ShortComplex.quasiIso_iff, exactAt_iff,
    ShortComplex.exact_iff_isZero_homology] at hK âŠ¢
  constructor
  Â· intro h
    exact IsZero.of_iso hK (@asIso _ _ _ _ _ h).symm
  Â· intro hL
    exact âŸ¨âŸ¨0, IsZero.eq_of_src hK _ _, IsZero.eq_of_tgt hL _ _âŸ©âŸ©

lemma quasiIsoAt_iff_exactAt' (f : K âŸ¶ L) (i : Î¹) [K.HasHomology i] [L.HasHomology i]
    (hL : L.ExactAt i) :
    QuasiIsoAt f i â†” K.ExactAt i := by
  simp only [quasiIsoAt_iff, ShortComplex.quasiIso_iff, exactAt_iff,
    ShortComplex.exact_iff_isZero_homology] at hL âŠ¢
  constructor
  Â· intro h
    exact IsZero.of_iso hL (@asIso _ _ _ _ _ h)
  Â· intro hK
    exact âŸ¨âŸ¨0, IsZero.eq_of_src hK _ _, IsZero.eq_of_tgt hL _ _âŸ©âŸ©

instance (f : K âŸ¶ L) (i : Î¹) [K.HasHomology i] [L.HasHomology i] [hf : QuasiIsoAt f i] :
    IsIso (homologyMap f i) := by
  simpa only [quasiIsoAt_iff, ShortComplex.quasiIso_iff] using hf

/-- The isomorphism `K.homology i â‰… L.homology i` induced by a morphism `f : K âŸ¶ L` such
that `[QuasiIsoAt f i]` holds. -/
@[simps! hom]
noncomputable def isoOfQuasiIsoAt (f : K âŸ¶ L) (i : Î¹) [K.HasHomology i] [L.HasHomology i]
    [QuasiIsoAt f i] : K.homology i â‰… L.homology i :=
  asIso (homologyMap f i)

@[reassoc (attr := simp)]
lemma isoOfQuasiIsoAt_hom_inv_id (f : K âŸ¶ L) (i : Î¹) [K.HasHomology i] [L.HasHomology i]
    [QuasiIsoAt f i] :
    homologyMap f i â‰« (isoOfQuasiIsoAt f i).inv = ğŸ™ _ :=
  (isoOfQuasiIsoAt f i).hom_inv_id

@[reassoc (attr := simp)]
lemma isoOfQuasiIsoAt_inv_hom_id (f : K âŸ¶ L) (i : Î¹) [K.HasHomology i] [L.HasHomology i]
    [QuasiIsoAt f i] :
    (isoOfQuasiIsoAt f i).inv â‰« homologyMap f i = ğŸ™ _ :=
  (isoOfQuasiIsoAt f i).inv_hom_id

lemma CochainComplex.quasiIsoAtâ‚€_iff {K L : CochainComplex C â„•} (f : K âŸ¶ L)
    [K.HasHomology 0] [L.HasHomology 0] [(K.sc' 0 0 1).HasHomology] [(L.sc' 0 0 1).HasHomology] :
    QuasiIsoAt f 0 â†”
      ShortComplex.QuasiIso ((HomologicalComplex.shortComplexFunctor' C _ 0 0 1).map f) :=
  quasiIsoAt_iff' _ _ _ _ (by simp) (by simp)

lemma ChainComplex.quasiIsoAtâ‚€_iff {K L : ChainComplex C â„•} (f : K âŸ¶ L)
    [K.HasHomology 0] [L.HasHomology 0] [(K.sc' 1 0 0).HasHomology] [(L.sc' 1 0 0).HasHomology] :
    QuasiIsoAt f 0 â†”
      ShortComplex.QuasiIso ((HomologicalComplex.shortComplexFunctor' C _ 1 0 0).map f) :=
  quasiIsoAt_iff' _ _ _ _ (by simp) (by simp)

/-- A morphism of homological complexes `f : K âŸ¶ L` is a quasi-isomorphism when it
is so in every degree, i.e. when the induced maps `homologyMap f i : K.homology i âŸ¶ L.homology i`
are all isomorphisms (see `quasiIso_iff` and `quasiIsoAt_iff_isIso_homologyMap`). -/
class QuasiIso (f : K âŸ¶ L) [âˆ€ i, K.HasHomology i] [âˆ€ i, L.HasHomology i] : Prop where
  quasiIsoAt : âˆ€ i, QuasiIsoAt f i := by infer_instance

lemma quasiIso_iff (f : K âŸ¶ L) [âˆ€ i, K.HasHomology i] [âˆ€ i, L.HasHomology i] :
    QuasiIso f â†” âˆ€ i, QuasiIsoAt f i :=
  âŸ¨fun h => h.quasiIsoAt, fun h => âŸ¨hâŸ©âŸ©

attribute [instance] QuasiIso.quasiIsoAt

instance quasiIso_of_isIso (f : K âŸ¶ L) [IsIso f] [âˆ€ i, K.HasHomology i] [âˆ€ i, L.HasHomology i] :
    QuasiIso f where

instance quasiIsoAt_comp (Ï† : K âŸ¶ L) (Ï†' : L âŸ¶ M) (i : Î¹) [K.HasHomology i]
    [L.HasHomology i] [M.HasHomology i]
    [hÏ† : QuasiIsoAt Ï† i] [hÏ†' : QuasiIsoAt Ï†' i] :
    QuasiIsoAt (Ï† â‰« Ï†') i := by
  rw [quasiIsoAt_iff] at hÏ† hÏ†' âŠ¢
  rw [Functor.map_comp]
  exact ShortComplex.quasiIso_comp _ _

instance quasiIso_comp (Ï† : K âŸ¶ L) (Ï†' : L âŸ¶ M) [âˆ€ i, K.HasHomology i]
    [âˆ€ i, L.HasHomology i] [âˆ€ i, M.HasHomology i]
    [hÏ† : QuasiIso Ï†] [hÏ†' : QuasiIso Ï†'] :
    QuasiIso (Ï† â‰« Ï†') where

lemma quasiIsoAt_of_comp_left (Ï† : K âŸ¶ L) (Ï†' : L âŸ¶ M) (i : Î¹) [K.HasHomology i]
    [L.HasHomology i] [M.HasHomology i]
    [hÏ† : QuasiIsoAt Ï† i] [hÏ†Ï†' : QuasiIsoAt (Ï† â‰« Ï†') i] :
    QuasiIsoAt Ï†' i := by
  rw [quasiIsoAt_iff_isIso_homologyMap] at hÏ† hÏ†Ï†' âŠ¢
  rw [homologyMap_comp] at hÏ†Ï†'
  exact IsIso.of_isIso_comp_left (homologyMap Ï† i) (homologyMap Ï†' i)

lemma quasiIsoAt_iff_comp_left (Ï† : K âŸ¶ L) (Ï†' : L âŸ¶ M) (i : Î¹) [K.HasHomology i]
    [L.HasHomology i] [M.HasHomology i]
    [hÏ† : QuasiIsoAt Ï† i] :
    QuasiIsoAt (Ï† â‰« Ï†') i â†” QuasiIsoAt Ï†' i := by
  constructor
  Â· intro
    exact quasiIsoAt_of_comp_left Ï† Ï†' i
  Â· intro
    infer_instance

lemma quasiIso_iff_comp_left (Ï† : K âŸ¶ L) (Ï†' : L âŸ¶ M) [âˆ€ i, K.HasHomology i]
    [âˆ€ i, L.HasHomology i] [âˆ€ i, M.HasHomology i]
    [hÏ† : QuasiIso Ï†] :
    QuasiIso (Ï† â‰« Ï†') â†” QuasiIso Ï†' := by
  simp only [quasiIso_iff, quasiIsoAt_iff_comp_left Ï† Ï†']

lemma quasiIso_of_comp_left (Ï† : K âŸ¶ L) (Ï†' : L âŸ¶ M) [âˆ€ i, K.HasHomology i]
    [âˆ€ i, L.HasHomology i] [âˆ€ i, M.HasHomology i]
    [hÏ† : QuasiIso Ï†] [hÏ†Ï†' : QuasiIso (Ï† â‰« Ï†')] :
    QuasiIso Ï†' := by
  rw [â† quasiIso_iff_comp_left Ï† Ï†']
  infer_instance

lemma quasiIsoAt_of_comp_right (Ï† : K âŸ¶ L) (Ï†' : L âŸ¶ M) (i : Î¹) [K.HasHomology i]
    [L.HasHomology i] [M.HasHomology i]
    [hÏ†' : QuasiIsoAt Ï†' i] [hÏ†Ï†' : QuasiIsoAt (Ï† â‰« Ï†') i] :
    QuasiIsoAt Ï† i := by
  rw [quasiIsoAt_iff_isIso_homologyMap] at hÏ†' hÏ†Ï†' âŠ¢
  rw [homologyMap_comp] at hÏ†Ï†'
  exact IsIso.of_isIso_comp_right (homologyMap Ï† i) (homologyMap Ï†' i)

lemma quasiIsoAt_iff_comp_right (Ï† : K âŸ¶ L) (Ï†' : L âŸ¶ M) (i : Î¹) [K.HasHomology i]
    [L.HasHomology i] [M.HasHomology i]
    [hÏ†' : QuasiIsoAt Ï†' i] :
    QuasiIsoAt (Ï† â‰« Ï†') i â†” QuasiIsoAt Ï† i := by
  constructor
  Â· intro
    exact quasiIsoAt_of_comp_right Ï† Ï†' i
  Â· intro
    infer_instance

lemma quasiIso_iff_comp_right (Ï† : K âŸ¶ L) (Ï†' : L âŸ¶ M) [âˆ€ i, K.HasHomology i]
    [âˆ€ i, L.HasHomology i] [âˆ€ i, M.HasHomology i]
    [hÏ†' : QuasiIso Ï†'] :
    QuasiIso (Ï† â‰« Ï†') â†” QuasiIso Ï† := by
  simp only [quasiIso_iff, quasiIsoAt_iff_comp_right Ï† Ï†']

lemma quasiIso_of_comp_right (Ï† : K âŸ¶ L) (Ï†' : L âŸ¶ M) [âˆ€ i, K.HasHomology i]
    [âˆ€ i, L.HasHomology i] [âˆ€ i, M.HasHomology i]
    [hÏ† : QuasiIso Ï†'] [hÏ†Ï†' : QuasiIso (Ï† â‰« Ï†')] :
    QuasiIso Ï† := by
  rw [â† quasiIso_iff_comp_right Ï† Ï†']
  infer_instance

lemma quasiIso_iff_of_arrow_mk_iso (Ï† : K âŸ¶ L) (Ï†' : K' âŸ¶ L') (e : Arrow.mk Ï† â‰… Arrow.mk Ï†')
    [âˆ€ i, K.HasHomology i] [âˆ€ i, L.HasHomology i]
    [âˆ€ i, K'.HasHomology i] [âˆ€ i, L'.HasHomology i] :
    QuasiIso Ï† â†” QuasiIso Ï†' := by
  rw [â† quasiIso_iff_comp_left (show K' âŸ¶ K from e.inv.left) Ï†,
    â† quasiIso_iff_comp_right Ï†' (show L' âŸ¶ L from e.inv.right)]
  erw [Arrow.w e.inv]
  rfl

lemma quasiIso_of_arrow_mk_iso (Ï† : K âŸ¶ L) (Ï†' : K' âŸ¶ L') (e : Arrow.mk Ï† â‰… Arrow.mk Ï†')
    [âˆ€ i, K.HasHomology i] [âˆ€ i, L.HasHomology i]
    [âˆ€ i, K'.HasHomology i] [âˆ€ i, L'.HasHomology i]
    [hÏ† : QuasiIso Ï†] : QuasiIso Ï†' := by
  simpa only [â† quasiIso_iff_of_arrow_mk_iso Ï† Ï†' e]
