/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, JoÃ«l Riou
-/
import Mathlib.Algebra.Homology.Homotopy
import Mathlib.CategoryTheory.Abelian.Homology

#align_import algebra.homology.quasi_iso from "leanprover-community/mathlib"@"956af7c76589f444f2e1313911bad16366ea476d"

/-!
# Quasi-isomorphisms

A chain map is a quasi-isomorphism if it induces isomorphisms on homology.

## Future work

Define the derived category as the localization at quasi-isomorphisms?
-/


open CategoryTheory

open CategoryTheory.Limits

universe v u

variable {Î¹ : Type*}

variable {V : Type u} [Category.{v} V] [HasZeroMorphisms V] [HasZeroObject V]

variable [HasEqualizers V] [HasImages V] [HasImageMaps V] [HasCokernels V]

variable {c : ComplexShape Î¹} {C D E : HomologicalComplex V c}

/-- A chain map is a quasi-isomorphism if it induces isomorphisms on homology.
-/
class QuasiIso (f : C âŸ¶ D) : Prop where
  IsIso : âˆ€ i, IsIso ((homologyFunctor V c i).map f)
#align quasi_iso QuasiIso

attribute [instance] QuasiIso.IsIso

instance (priority := 100) quasiIso_of_iso (f : C âŸ¶ D) [IsIso f] : QuasiIso f where
  IsIso i := by
    change IsIso ((homologyFunctor V c i).mapIso (asIso f)).hom
    -- âŠ¢ IsIso ((homologyFunctor V c i).mapIso (asIso f)).hom
    infer_instance
    -- ğŸ‰ no goals
#align quasi_iso_of_iso quasiIso_of_iso

instance quasiIso_comp (f : C âŸ¶ D) [QuasiIso f] (g : D âŸ¶ E) [QuasiIso g] : QuasiIso (f â‰« g) where
  IsIso i := by
    rw [Functor.map_comp]
    -- âŠ¢ IsIso ((homologyFunctor V c i).map f â‰« (homologyFunctor V c i).map g)
    infer_instance
    -- ğŸ‰ no goals
#align quasi_iso_comp quasiIso_comp

theorem quasiIso_of_comp_left (f : C âŸ¶ D) [QuasiIso f] (g : D âŸ¶ E) [QuasiIso (f â‰« g)] :
    QuasiIso g :=
  { IsIso := fun i => IsIso.of_isIso_fac_left ((homologyFunctor V c i).map_comp f g).symm }
#align quasi_iso_of_comp_left quasiIso_of_comp_left

theorem quasiIso_of_comp_right (f : C âŸ¶ D) (g : D âŸ¶ E) [QuasiIso g] [QuasiIso (f â‰« g)] :
    QuasiIso f :=
  { IsIso := fun i => IsIso.of_isIso_fac_right ((homologyFunctor V c i).map_comp f g).symm }
#align quasi_iso_of_comp_right quasiIso_of_comp_right

namespace HomotopyEquiv

section

variable {W : Type*} [Category W] [Preadditive W] [HasCokernels W] [HasImages W] [HasEqualizers W]
  [HasZeroObject W] [HasImageMaps W]

/-- A homotopy equivalence is a quasi-isomorphism. -/
theorem toQuasiIso {C D : HomologicalComplex W c} (e : HomotopyEquiv C D) : QuasiIso e.hom :=
  âŸ¨fun i => by
    refine' âŸ¨âŸ¨(homologyFunctor W c i).map e.inv, _âŸ©âŸ©
    -- âŠ¢ (homologyFunctor W c i).map e.hom â‰« (homologyFunctor W c i).map e.inv = ğŸ™ (( â€¦
    simp only [â† Functor.map_comp, â† (homologyFunctor W c i).map_id]
    -- âŠ¢ (homologyFunctor W c i).map (e.hom â‰« e.inv) = (homologyFunctor W c i).map (ğŸ™ â€¦
    constructor <;> apply homology_map_eq_of_homotopy
    -- âŠ¢ (homologyFunctor W c i).map (e.hom â‰« e.inv) = (homologyFunctor W c i).map (ğŸ™ â€¦
                    -- âŠ¢ Homotopy (e.hom â‰« e.inv) (ğŸ™ C)
                    -- âŠ¢ Homotopy (e.inv â‰« e.hom) (ğŸ™ D)
    exacts [e.homotopyHomInvId, e.homotopyInvHomId]âŸ©
    -- ğŸ‰ no goals
#align homotopy_equiv.to_quasi_iso HomotopyEquiv.toQuasiIso

theorem toQuasiIso_inv {C D : HomologicalComplex W c} (e : HomotopyEquiv C D) (i : Î¹) :
    (@asIso _ _ _ _ _ (e.toQuasiIso.1 i)).inv = (homologyFunctor W c i).map e.inv := by
  symm
  -- âŠ¢ (homologyFunctor W c i).map e.inv = (asIso ((homologyFunctor W c i).map e.ho â€¦
  haveI := e.toQuasiIso.1 i -- Porting note: Added this to get `asIso_hom` to work.
  -- âŠ¢ (homologyFunctor W c i).map e.inv = (asIso ((homologyFunctor W c i).map e.ho â€¦
  simp only [â† Iso.hom_comp_eq_id, asIso_hom, â† Functor.map_comp, â† (homologyFunctor W c i).map_id,
    homology_map_eq_of_homotopy e.homotopyHomInvId _]
#align homotopy_equiv.to_quasi_iso_inv HomotopyEquiv.toQuasiIso_inv

end

end HomotopyEquiv

namespace HomologicalComplex.Hom

section ToSingleâ‚€

variable {W : Type*} [Category W] [Abelian W]

section

variable {X : ChainComplex W â„•} {Y : W} (f : X âŸ¶ (ChainComplex.singleâ‚€ _).obj Y) [hf : QuasiIso f]

/-- If a chain map `f : X âŸ¶ Y[0]` is a quasi-isomorphism, then the cokernel of the differential
`d : Xâ‚ â†’ Xâ‚€` is isomorphic to `Y`. -/
noncomputable def toSingleâ‚€CokernelAtZeroIso : cokernel (X.d 1 0) â‰… Y :=
  X.homologyZeroIso.symm.trans
    ((@asIso _ _ _ _ _ (hf.1 0)).trans ((ChainComplex.homologyFunctor0Singleâ‚€ W).app Y))
#align homological_complex.hom.to_singleâ‚€_cokernel_at_zero_iso HomologicalComplex.Hom.toSingleâ‚€CokernelAtZeroIso

theorem toSingleâ‚€CokernelAtZeroIso_hom_eq [hf : QuasiIso f] :
    f.toSingleâ‚€CokernelAtZeroIso.hom =
      cokernel.desc (X.d 1 0) (f.f 0) (by rw [â† f.2 1 0 rfl]; exact comp_zero) := by
                                          -- âŠ¢ HomologicalComplex.Hom.f f 1 â‰« d ((ChainComplex.singleâ‚€ W).obj Y) 1 0 = 0
                                                              -- ğŸ‰ no goals
  ext
  -- âŠ¢ coequalizer.Ï€ (d X 1 0) 0 â‰« (toSingleâ‚€CokernelAtZeroIso f).hom = coequalizer â€¦
  dsimp only [toSingleâ‚€CokernelAtZeroIso, ChainComplex.homologyZeroIso, homologyOfZeroRight,
    homology.mapIso, ChainComplex.homologyFunctor0Singleâ‚€, cokernel.map]
  dsimp [asIso]
  -- âŠ¢ cokernel.Ï€ (d X 1 0) â‰« ((cokernel.desc (d X 1 0) (cokernel.Ï€ (image.Î¹ (d X 1 â€¦
  simp only [cokernel.Ï€_desc, Category.assoc, homology.map_desc, cokernel.Ï€_desc_assoc]
  -- âŠ¢ kernelZeroIsoSource.inv â‰« (kernelSubobjectIso 0).inv â‰« cokernel.Ï€ (imageToKe â€¦
  simp [homology.desc, Iso.refl_inv (X.X 0)]
  -- ğŸ‰ no goals
#align homological_complex.hom.to_singleâ‚€_cokernel_at_zero_iso_hom_eq HomologicalComplex.Hom.toSingleâ‚€CokernelAtZeroIso_hom_eq

theorem to_singleâ‚€_epi_at_zero [hf : QuasiIso f] : Epi (f.f 0) := by
  constructor
  -- âŠ¢ âˆ€ {Z : W} (g h : HomologicalComplex.X ((ChainComplex.singleâ‚€ W).obj Y) 0 âŸ¶ Z â€¦
  intro Z g h Hgh
  -- âŠ¢ g = h
  rw [â† cokernel.Ï€_desc (X.d 1 0) (f.f 0) (by rw [â† f.2 1 0 rfl]; exact comp_zero),
    â† toSingleâ‚€CokernelAtZeroIso_hom_eq] at Hgh
  rw [(@cancel_epi _ _ _ _ _ _ (epi_comp _ _) _ _).1 Hgh]
  -- ğŸ‰ no goals
#align homological_complex.hom.to_singleâ‚€_epi_at_zero HomologicalComplex.Hom.to_singleâ‚€_epi_at_zero

theorem to_singleâ‚€_exact_d_f_at_zero [hf : QuasiIso f] : Exact (X.d 1 0) (f.f 0) := by
  rw [Preadditive.exact_iff_homology_zero]
  -- âŠ¢ âˆƒ w, Nonempty (_root_.homology (d X 1 0) (HomologicalComplex.Hom.f f 0) w â‰… 0)
  have h : X.d 1 0 â‰« f.f 0 = 0 := by
    simp only [â† f.2 1 0 rfl, ChainComplex.singleâ‚€_obj_X_d, comp_zero]
  refine' âŸ¨h, Nonempty.intro (homologyIsoKernelDesc _ _ _ â‰ªâ‰« _)âŸ©
  -- âŠ¢ kernel (cokernel.desc (d X 1 0) (HomologicalComplex.Hom.f f 0) h) â‰… 0
  suffices IsIso (cokernel.desc _ _ h) by apply kernel.ofMono
  -- âŠ¢ IsIso (cokernel.desc (d X 1 0) (HomologicalComplex.Hom.f f 0) h)
  rw [â† toSingleâ‚€CokernelAtZeroIso_hom_eq]
  -- âŠ¢ IsIso (toSingleâ‚€CokernelAtZeroIso f).hom
  infer_instance
  -- ğŸ‰ no goals
#align homological_complex.hom.to_singleâ‚€_exact_d_f_at_zero HomologicalComplex.Hom.to_singleâ‚€_exact_d_f_at_zero

theorem to_singleâ‚€_exact_at_succ [hf : QuasiIso f] (n : â„•) :
    Exact (X.d (n + 2) (n + 1)) (X.d (n + 1) n) :=
  (Preadditive.exact_iff_homology_zero _ _).2
    âŸ¨X.d_comp_d _ _ _,
      âŸ¨(ChainComplex.homologySuccIso _ _).symm.trans
          ((@asIso _ _ _ _ _ (hf.1 (n + 1))).trans homologyZeroZero)âŸ©âŸ©
#align homological_complex.hom.to_singleâ‚€_exact_at_succ HomologicalComplex.Hom.to_singleâ‚€_exact_at_succ

end

section

variable {X : CochainComplex W â„•} {Y : W} (f : (CochainComplex.singleâ‚€ _).obj Y âŸ¶ X)

/-- If a cochain map `f : Y[0] âŸ¶ X` is a quasi-isomorphism, then the kernel of the differential
`d : Xâ‚€ â†’ Xâ‚` is isomorphic to `Y`. -/
noncomputable def fromSingleâ‚€KernelAtZeroIso [hf : QuasiIso f] : kernel (X.d 0 1) â‰… Y :=
  X.homologyZeroIso.symm.trans
    ((@asIso _ _ _ _ _ (hf.1 0)).symm.trans ((CochainComplex.homologyFunctor0Singleâ‚€ W).app Y))
#align homological_complex.hom.from_singleâ‚€_kernel_at_zero_iso HomologicalComplex.Hom.fromSingleâ‚€KernelAtZeroIso

theorem fromSingleâ‚€KernelAtZeroIso_inv_eq [hf : QuasiIso f] :
    f.fromSingleâ‚€KernelAtZeroIso.inv =
      kernel.lift (X.d 0 1) (f.f 0) (by rw [f.2 0 1 rfl]; exact zero_comp) := by
                                        -- âŠ¢ d ((CochainComplex.singleâ‚€ W).obj Y) 0 1 â‰« HomologicalComplex.Hom.f f 1 = 0
                                                          -- ğŸ‰ no goals
  ext
  -- âŠ¢ (fromSingleâ‚€KernelAtZeroIso f).inv â‰« equalizer.Î¹ (d X 0 1) 0 = kernel.lift ( â€¦
  dsimp only [fromSingleâ‚€KernelAtZeroIso, CochainComplex.homologyZeroIso, homologyOfZeroLeft,
    homology.mapIso, CochainComplex.homologyFunctor0Singleâ‚€, kernel.map]
  simp only [Iso.trans_inv, Iso.app_inv, Iso.symm_inv, Category.assoc, equalizer_as_kernel,
    kernel.lift_Î¹]
  dsimp [asIso]
  -- âŠ¢ ((inv (Subobject.arrow (kernelSubobject 0)) â‰« homology.Ï€ 0 0 (_ : 0 â‰« 0 = 0) â€¦
  simp only [Category.assoc, homology.Ï€_map, cokernelZeroIsoTarget_hom,
    cokernelIsoOfEq_hom_comp_desc, kernelSubobject_arrow, homology.Ï€_map_assoc, IsIso.inv_comp_eq]
  simp [homology.Ï€, kernelSubobjectMap_comp, Iso.refl_hom (X.X 0), Category.comp_id]
  -- ğŸ‰ no goals
#align homological_complex.hom.from_singleâ‚€_kernel_at_zero_iso_inv_eq HomologicalComplex.Hom.fromSingleâ‚€KernelAtZeroIso_inv_eq

theorem from_singleâ‚€_mono_at_zero [hf : QuasiIso f] : Mono (f.f 0) := by
  constructor
  -- âŠ¢ âˆ€ {Z : W} (g h : Z âŸ¶ HomologicalComplex.X ((CochainComplex.singleâ‚€ W).obj Y) â€¦
  intro Z g h Hgh
  -- âŠ¢ g = h
  rw [â† kernel.lift_Î¹ (X.d 0 1) (f.f 0) (by rw [f.2 0 1 rfl]; exact zero_comp),
    â† fromSingleâ‚€KernelAtZeroIso_inv_eq] at Hgh
  rw [(@cancel_mono _ _ _ _ _ _ (mono_comp _ _) _ _).1 Hgh]
  -- ğŸ‰ no goals
#align homological_complex.hom.from_singleâ‚€_mono_at_zero HomologicalComplex.Hom.from_singleâ‚€_mono_at_zero

theorem from_singleâ‚€_exact_f_d_at_zero [hf : QuasiIso f] : Exact (f.f 0) (X.d 0 1) := by
  rw [Preadditive.exact_iff_homology_zero]
  -- âŠ¢ âˆƒ w, Nonempty (_root_.homology (HomologicalComplex.Hom.f f 0) (d X 0 1) w â‰… 0)
  have h : f.f 0 â‰« X.d 0 1 = 0 := by
    simp only [HomologicalComplex.Hom.comm, CochainComplex.singleâ‚€_obj_X_d, zero_comp]
  refine' âŸ¨h, Nonempty.intro (homologyIsoCokernelLift _ _ _ â‰ªâ‰« _)âŸ©
  -- âŠ¢ cokernel (kernel.lift (d X 0 1) (HomologicalComplex.Hom.f f 0) h) â‰… 0
  suffices IsIso (kernel.lift (X.d 0 1) (f.f 0) h) by apply cokernel.ofEpi
  -- âŠ¢ IsIso (kernel.lift (d X 0 1) (HomologicalComplex.Hom.f f 0) h)
  rw [â† fromSingleâ‚€KernelAtZeroIso_inv_eq f]
  -- âŠ¢ IsIso (fromSingleâ‚€KernelAtZeroIso f).inv
  infer_instance
  -- ğŸ‰ no goals
#align homological_complex.hom.from_singleâ‚€_exact_f_d_at_zero HomologicalComplex.Hom.from_singleâ‚€_exact_f_d_at_zero

theorem from_singleâ‚€_exact_at_succ [hf : QuasiIso f] (n : â„•) :
    Exact (X.d n (n + 1)) (X.d (n + 1) (n + 2)) :=
  (Preadditive.exact_iff_homology_zero _ _).2
    âŸ¨X.d_comp_d _ _ _,
      âŸ¨(CochainComplex.homologySuccIso _ _).symm.trans
          ((@asIso _ _ _ _ _ (hf.1 (n + 1))).symm.trans homologyZeroZero)âŸ©âŸ©
#align homological_complex.hom.from_singleâ‚€_exact_at_succ HomologicalComplex.Hom.from_singleâ‚€_exact_at_succ

end

end ToSingleâ‚€

end HomologicalComplex.Hom

variable {A : Type*} [Category A] [Abelian A] {B : Type*} [Category B] [Abelian B] (F : A â¥¤ B)
  [Functor.Additive F] [PreservesFiniteLimits F] [PreservesFiniteColimits F] [Faithful F]

theorem CategoryTheory.Functor.quasiIso_of_map_quasiIso {C D : HomologicalComplex A c} (f : C âŸ¶ D)
    (hf : QuasiIso ((F.mapHomologicalComplex _).map f)) : QuasiIso f :=
  âŸ¨fun i =>
    haveI : IsIso (F.map ((homologyFunctor A c i).map f)) := by
      rw [â† Functor.comp_map, â† NatIso.naturality_2 (F.homologyFunctorIso i) f, Functor.comp_map]
      -- âŠ¢ IsIso (NatTrans.app (homologyFunctorIso F i).hom C â‰« (homologyFunctor B c i) â€¦
      infer_instance
      -- ğŸ‰ no goals
    isIso_of_reflects_iso _ FâŸ©
#align category_theory.functor.quasi_iso_of_map_quasi_iso CategoryTheory.Functor.quasiIso_of_map_quasiIso
