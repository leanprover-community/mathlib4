/-
Copyright (c) 2021 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.Algebra.Group.InjSurj
import Mathlib.Data.List.BigOperators.Basic
import Mathlib.Data.List.FinRange
import Mathlib.GroupTheory.GroupAction.Defs
import Mathlib.GroupTheory.Submonoid.Basic
import Mathlib.Data.SetLike.Basic
import Mathlib.Data.Sigma.Basic
import Lean.Elab.Tactic

#align_import algebra.graded_monoid from "leanprover-community/mathlib"@"008205aa645b3f194c1da47025c5f110c8406eab"

/-!
# Additively-graded multiplicative structures

This module provides a set of heterogeneous typeclasses for defining a multiplicative structure
over the sigma type `GradedMonoid A` such that `(*) : A i â†’ A j â†’ A (i + j)`; that is to say, `A`
forms an additively-graded monoid. The typeclasses are:

* `GradedMonoid.GOne A`
* `GradedMonoid.GMul A`
* `GradedMonoid.GMonoid A`
* `GradedMonoid.GCommMonoid A`

With the `SigmaGraded` locale open, these respectively imbue:

* `One (GradedMonoid A)`
* `Mul (GradedMonoid A)`
* `Monoid (GradedMonoid A)`
* `CommMonoid (GradedMonoid A)`

the base type `A 0` with:

* `GradedMonoid.GradeZero.One`
* `GradedMonoid.GradeZero.Mul`
* `GradedMonoid.GradeZero.Monoid`
* `GradedMonoid.GradeZero.CommMonoid`

and the `i`th grade `A i` with `A 0`-actions (`â€¢`) defined as left-multiplication:

* (nothing)
* `GradedMonoid.GradeZero.SMul (A 0)`
* `GradedMonoid.GradeZero.MulAction (A 0)`
* (nothing)

For now, these typeclasses are primarily used in the construction of `DirectSum.Ring` and the rest
of that file.

## Dependent graded products

This also introduces `List.dProd`, which takes the (possibly non-commutative) product of a list
of graded elements of type `A i`. This definition primarily exist to allow `GradedMonoid.mk`
and `DirectSum.of` to be pulled outside a product, such as in `GradedMonoid.mk_list_dProd` and
`DirectSum.of_list_dProd`.

## Internally graded monoids

In addition to the above typeclasses, in the most frequent case when `A` is an indexed collection of
`SetLike` subobjects (such as `AddSubmonoid`s, `AddSubgroup`s, or `Submodule`s), this file
provides the `Prop` typeclasses:

* `SetLike.GradedOne A` (which provides the obvious `GradedMonoid.GOne A` instance)
* `SetLike.GradedMul A` (which provides the obvious `GradedMonoid.GMul A` instance)
* `SetLike.GradedMonoid A` (which provides the obvious `GradedMonoid.GMonoid A` and
  `GradedMonoid.GCommMonoid A` instances)

which respectively provide the API lemmas

* `SetLike.one_mem_graded`
* `SetLike.mul_mem_graded`
* `SetLike.pow_mem_graded`, `SetLike.list_prod_map_mem_graded`

Strictly this last class is unnecessary as it has no fields not present in its parents, but it is
included for convenience. Note that there is no need for `SetLike.GradedRing` or similar, as all
the information it would contain is already supplied by `GradedMonoid` when `A` is a collection
of objects satisfying `AddSubmonoidClass` such as `Submodule`s. These constructions are explored
in `Algebra.DirectSum.Internal`.

This file also defines:

* `SetLike.isHomogeneous A` (which says that `a` is homogeneous iff `a âˆˆ A i` for some `i : Î¹`)
* `SetLike.homogeneousSubmonoid A`, which is, as the name suggests, the submonoid consisting of
  all the homogeneous elements.

## tags

graded monoid
-/


variable {Î¹ : Type*}

/-- A type alias of sigma types for graded monoids. -/
def GradedMonoid (A : Î¹ â†’ Type*) :=
  Sigma A
#align graded_monoid GradedMonoid

namespace GradedMonoid

instance {A : Î¹ â†’ Type*} [Inhabited Î¹] [Inhabited (A default)] : Inhabited (GradedMonoid A) :=
  inferInstanceAs <| Inhabited (Sigma _)

/-- Construct an element of a graded monoid. -/
def mk {A : Î¹ â†’ Type*} : âˆ€ i, A i â†’ GradedMonoid A :=
  Sigma.mk
#align graded_monoid.mk GradedMonoid.mk

/-! ### Typeclasses -/

section Defs

variable (A : Î¹ â†’ Type*)

/-- A graded version of `One`, which must be of grade 0. -/
class GOne [Zero Î¹] where
  /-- The term `one` of grade 0 -/
  one : A 0
#align graded_monoid.ghas_one GradedMonoid.GOne

/-- `GOne` implies `One (GradedMonoid A)` -/
instance GOne.toOne [Zero Î¹] [GOne A] : One (GradedMonoid A) :=
  âŸ¨âŸ¨_, GOne.oneâŸ©âŸ©
#align graded_monoid.ghas_one.to_has_one GradedMonoid.GOne.toOne

/-- A graded version of `Mul`. Multiplication combines grades additively, like
`AddMonoidAlgebra`. -/
class GMul [Add Î¹] where
  /-- The homogeneous multiplication map `mul` -/
  mul {i j} : A i â†’ A j â†’ A (i + j)
#align graded_monoid.ghas_mul GradedMonoid.GMul

/-- `GMul` implies `Mul (GradedMonoid A)`. -/
instance GMul.toMul [Add Î¹] [GMul A] : Mul (GradedMonoid A) :=
  âŸ¨fun x y : GradedMonoid A => âŸ¨_, GMul.mul x.snd y.sndâŸ©âŸ©
#align graded_monoid.ghas_mul.to_has_mul GradedMonoid.GMul.toMul

theorem mk_mul_mk [Add Î¹] [GMul A] {i j} (a : A i) (b : A j) :
    mk i a * mk j b = mk (i + j) (GMul.mul a b) :=
  rfl
#align graded_monoid.mk_mul_mk GradedMonoid.mk_mul_mk

namespace GMonoid

variable {A} [AddMonoid Î¹] [GMul A] [GOne A]

/-- A default implementation of power on a graded monoid, like `npowRec`.
`GMonoid.gnpow` should be used instead. -/
def gnpowRec : âˆ€ (n : â„•) {i}, A i â†’ A (n â€¢ i)
  | 0, i, _ => cast (congr_arg A (zero_nsmul i).symm) GOne.one
  | n + 1, i, a => cast (congr_arg A (succ_nsmul i n).symm) (GMul.mul a <| gnpowRec _ a)
#align graded_monoid.gmonoid.gnpow_rec GradedMonoid.GMonoid.gnpowRec

@[simp]
theorem gnpowRec_zero (a : GradedMonoid A) : GradedMonoid.mk _ (gnpowRec 0 a.snd) = 1 :=
  Sigma.ext (zero_nsmul _) (heq_of_cast_eq _ rfl).symm
#align graded_monoid.gmonoid.gnpow_rec_zero GradedMonoid.GMonoid.gnpowRec_zero

@[simp]
theorem gnpowRec_succ (n : â„•) (a : GradedMonoid A) :
    (GradedMonoid.mk _ <| gnpowRec n.succ a.snd) = a * âŸ¨_, gnpowRec n a.sndâŸ© :=
  Sigma.ext (succ_nsmul _ _) (heq_of_cast_eq _ rfl).symm
#align graded_monoid.gmonoid.gnpow_rec_succ GradedMonoid.GMonoid.gnpowRec_succ

end GMonoid

/-- A tactic to for use as an optional value for `GMonoid.gnpow_zero'`. -/
macro "apply_gmonoid_gnpowRec_zero_tac" : tactic => `(tactic| apply GMonoid.gnpowRec_zero)
/-- A tactic to for use as an optional value for `GMonoid.gnpow_succ'`. -/
macro "apply_gmonoid_gnpowRec_succ_tac" : tactic => `(tactic| apply GMonoid.gnpowRec_succ)

/-- A graded version of `Monoid`

Like `Monoid.npow`, this has an optional `GMonoid.gnpow` field to allow definitional control of
natural powers of a graded monoid. -/
class GMonoid [AddMonoid Î¹] extends GMul A, GOne A where
  /-- Multiplication by `one` on the left is the identity -/
  one_mul (a : GradedMonoid A) : 1 * a = a
  /-- Multiplication by `one` on the right is the identity -/
  mul_one (a : GradedMonoid A) : a * 1 = a
  /-- Multiplication is associative -/
  mul_assoc (a b c : GradedMonoid A) : a * b * c = a * (b * c)
  /-- Optional field to allow definitional control of natural powers -/
  gnpow : âˆ€ (n : â„•) {i}, A i â†’ A (n â€¢ i) := GMonoid.gnpowRec
  /-- The zeroth power will yield 1 -/
  gnpow_zero' : âˆ€ a : GradedMonoid A, GradedMonoid.mk _ (gnpow 0 a.snd) = 1 := by
    apply_gmonoid_gnpowRec_zero_tac
  /-- Successor powers behave as expected -/
  gnpow_succ' :
    âˆ€ (n : â„•) (a : GradedMonoid A),
      (GradedMonoid.mk _ <| gnpow n.succ a.snd) = a * âŸ¨_, gnpow n a.sndâŸ© := by
    apply_gmonoid_gnpowRec_succ_tac
#align graded_monoid.gmonoid GradedMonoid.GMonoid

/-- `GMonoid` implies a `Monoid (GradedMonoid A)`. -/
instance GMonoid.toMonoid [AddMonoid Î¹] [GMonoid A] : Monoid (GradedMonoid A)
    where
  one := 1
  mul := (Â· * Â·)
  npow n a := GradedMonoid.mk _ (GMonoid.gnpow n a.snd)
  npow_zero a := GMonoid.gnpow_zero' a
  npow_succ n a := GMonoid.gnpow_succ' n a
  one_mul := GMonoid.one_mul
  mul_one := GMonoid.mul_one
  mul_assoc := GMonoid.mul_assoc
#align graded_monoid.gmonoid.to_monoid GradedMonoid.GMonoid.toMonoid

theorem mk_pow [AddMonoid Î¹] [GMonoid A] {i} (a : A i) (n : â„•) :
    mk i a ^ n = mk (n â€¢ i) (GMonoid.gnpow _ a) := by
  match n with
  | 0 =>
    rw [pow_zero]
    exact (GMonoid.gnpow_zero' âŸ¨_, aâŸ©).symm
  | n+1 =>
    rw [pow_succ, mk_pow a n, mk_mul_mk]
    exact (GMonoid.gnpow_succ' n âŸ¨_, aâŸ©).symm
#align graded_monoid.mk_pow GradedMonoid.mk_pow

/-- A graded version of `CommMonoid`. -/
class GCommMonoid [AddCommMonoid Î¹] extends GMonoid A where
  /-- Multiplication is commutative -/
  mul_comm (a : GradedMonoid A) (b : GradedMonoid A) : a * b = b * a
#align graded_monoid.gcomm_monoid GradedMonoid.GCommMonoid

/-- `GCommMonoid` implies a `CommMonoid (GradedMonoid A)`, although this is only used as an
instance locally to define notation in `gmonoid` and similar typeclasses. -/
instance GCommMonoid.toCommMonoid [AddCommMonoid Î¹] [GCommMonoid A] : CommMonoid (GradedMonoid A) :=
  { GMonoid.toMonoid A with mul_comm := GCommMonoid.mul_comm }
#align graded_monoid.gcomm_monoid.to_comm_monoid GradedMonoid.GCommMonoid.toCommMonoid

end Defs

/-! ### Instances for `A 0`

The various `g*` instances are enough to promote the `AddCommMonoid (A 0)` structure to various
types of multiplicative structure.
-/


section GradeZero

variable (A : Î¹ â†’ Type*)

section One

variable [Zero Î¹] [GOne A]

/-- `1 : A 0` is the value provided in `GOne.one`. -/
@[nolint unusedArguments]
instance GradeZero.one : One (A 0) :=
  âŸ¨GOne.oneâŸ©
#align graded_monoid.grade_zero.has_one GradedMonoid.GradeZero.one

end One

section Mul

variable [AddZeroClass Î¹] [GMul A]

/-- `(â€¢) : A 0 â†’ A i â†’ A i` is the value provided in `GradedMonoid.GMul.mul`, composed with
an `Eq.rec` to turn `A (0 + i)` into `A i`.
-/
instance GradeZero.smul (i : Î¹) : SMul (A 0) (A i)
    where smul x y := @Eq.rec Î¹ (0+i) (fun a _ => A a) (GMul.mul x y) i (zero_add i)
#align graded_monoid.grade_zero.has_smul GradedMonoid.GradeZero.smul

/-- `(*) : A 0 â†’ A 0 â†’ A 0` is the value provided in `GradedMonoid.GMul.mul`, composed with
an `Eq.rec` to turn `A (0 + 0)` into `A 0`.
-/
instance GradeZero.mul : Mul (A 0) where mul := (Â· â€¢ Â·)
#align graded_monoid.grade_zero.has_mul GradedMonoid.GradeZero.mul

variable {A}

@[simp]
theorem mk_zero_smul {i} (a : A 0) (b : A i) : mk _ (a â€¢ b) = mk _ a * mk _ b :=
  Sigma.ext (zero_add _).symm <| eq_rec_heq _ _
#align graded_monoid.mk_zero_smul GradedMonoid.mk_zero_smul

@[simp]
theorem GradeZero.smul_eq_mul (a b : A 0) : a â€¢ b = a * b :=
  rfl
#align graded_monoid.grade_zero.smul_eq_mul GradedMonoid.GradeZero.smul_eq_mul

end Mul

section Monoid

variable [AddMonoid Î¹] [GMonoid A]

instance : Pow (A 0) â„• where
  pow x n := @Eq.rec Î¹ (n â€¢ (0:Î¹)) (fun a _ => A a) (GMonoid.gnpow n x) 0 (nsmul_zero n)

variable {A}

@[simp]
theorem mk_zero_pow (a : A 0) (n : â„•) : mk _ (a ^ n) = mk _ a ^ n :=
  Sigma.ext (nsmul_zero n).symm <| eq_rec_heq _ _
#align graded_monoid.mk_zero_pow GradedMonoid.mk_zero_pow

variable (A)

/-- The `Monoid` structure derived from `GMonoid A`. -/
instance GradeZero.monoid : Monoid (A 0) :=
  Function.Injective.monoid (mk 0) sigma_mk_injective rfl mk_zero_smul mk_zero_pow
#align graded_monoid.grade_zero.monoid GradedMonoid.GradeZero.monoid

end Monoid

section Monoid

variable [AddCommMonoid Î¹] [GCommMonoid A]

/-- The `CommMonoid` structure derived from `GCommMonoid A`. -/
instance GradeZero.commMonoid : CommMonoid (A 0) :=
  Function.Injective.commMonoid (mk 0) sigma_mk_injective rfl mk_zero_smul mk_zero_pow
#align graded_monoid.grade_zero.comm_monoid GradedMonoid.GradeZero.commMonoid

end Monoid

section MulAction

variable [AddMonoid Î¹] [GMonoid A]

/-- `GradedMonoid.mk 0` is a `MonoidHom`, using the `GradedMonoid.GradeZero.monoid` structure.
-/
def mkZeroMonoidHom : A 0 â†’* GradedMonoid A
    where
  toFun := mk 0
  map_one' := rfl
  map_mul' := mk_zero_smul
#align graded_monoid.mk_zero_monoid_hom GradedMonoid.mkZeroMonoidHom

/-- Each grade `A i` derives an `A 0`-action structure from `GMonoid A`. -/
instance GradeZero.mulAction {i} : MulAction (A 0) (A i) :=
  letI := MulAction.compHom (GradedMonoid A) (mkZeroMonoidHom A)
  Function.Injective.mulAction (mk i) sigma_mk_injective mk_zero_smul
#align graded_monoid.grade_zero.mul_action GradedMonoid.GradeZero.mulAction

end MulAction

end GradeZero

end GradedMonoid

/-! ### Dependent products of graded elements -/


section DProd

variable {Î± : Type*} {A : Î¹ â†’ Type*} [AddMonoid Î¹] [GradedMonoid.GMonoid A]

/-- The index used by `List.dProd`. Propositionally this is equal to `(l.map fÎ¹).Sum`, but
definitionally it needs to have a different form to avoid introducing `Eq.rec`s in `List.dProd`. -/
def List.dProdIndex (l : List Î±) (fÎ¹ : Î± â†’ Î¹) : Î¹ :=
  l.foldr (fun i b => fÎ¹ i + b) 0
#align list.dprod_index List.dProdIndex

@[simp]
theorem List.dProdIndex_nil (fÎ¹ : Î± â†’ Î¹) : ([] : List Î±).dProdIndex fÎ¹ = 0 :=
  rfl
#align list.dprod_index_nil List.dProdIndex_nil

@[simp]
theorem List.dProdIndex_cons (a : Î±) (l : List Î±) (fÎ¹ : Î± â†’ Î¹) :
    (a :: l).dProdIndex fÎ¹ = fÎ¹ a + l.dProdIndex fÎ¹ :=
  rfl
#align list.dprod_index_cons List.dProdIndex_cons

theorem List.dProdIndex_eq_map_sum (l : List Î±) (fÎ¹ : Î± â†’ Î¹) :
    l.dProdIndex fÎ¹ = (l.map fÎ¹).sum := by
  match l with
  | [] => simp
  | head::tail => simp [List.dProdIndex_eq_map_sum tail fÎ¹]
#align list.dprod_index_eq_map_sum List.dProdIndex_eq_map_sum

/-- A dependent product for graded monoids represented by the indexed family of types `A i`.
This is a dependent version of `(l.map fA).prod`.

For a list `l : List Î±`, this computes the product of `fA a` over `a`, where each `fA` is of type
`A (fÎ¹ a)`. -/
def List.dProd (l : List Î±) (fÎ¹ : Î± â†’ Î¹) (fA : âˆ€ a, A (fÎ¹ a)) : A (l.dProdIndex fÎ¹) :=
  l.foldrRecOn _ _ GradedMonoid.GOne.one fun _ x a _ => GradedMonoid.GMul.mul (fA a) x
#align list.dprod List.dProd

@[simp]
theorem List.dProd_nil (fÎ¹ : Î± â†’ Î¹) (fA : âˆ€ a, A (fÎ¹ a)) :
    (List.nil : List Î±).dProd fÎ¹ fA = GradedMonoid.GOne.one :=
  rfl
#align list.dprod_nil List.dProd_nil

-- the `( : _)` in this lemma statement results in the type on the RHS not being unfolded, which
-- is nicer in the goal view.
@[simp]
theorem List.dProd_cons (fÎ¹ : Î± â†’ Î¹) (fA : âˆ€ a, A (fÎ¹ a)) (a : Î±) (l : List Î±) :
    (a :: l).dProd fÎ¹ fA = (GradedMonoid.GMul.mul (fA a) (l.dProd fÎ¹ fA) : _) :=
  rfl
#align list.dprod_cons List.dProd_cons

theorem GradedMonoid.mk_list_dProd (l : List Î±) (fÎ¹ : Î± â†’ Î¹) (fA : âˆ€ a, A (fÎ¹ a)) :
    GradedMonoid.mk _ (l.dProd fÎ¹ fA) = (l.map fun a => GradedMonoid.mk (fÎ¹ a) (fA a)).prod := by
  match l with
  | [] => simp; rfl
  | head::tail =>
    simp[â† GradedMonoid.mk_list_dProd tail _ _, GradedMonoid.mk_mul_mk, List.prod_cons]
#align graded_monoid.mk_list_dprod GradedMonoid.mk_list_dProd

/-- A variant of `GradedMonoid.mk_list_dProd` for rewriting in the other direction. -/
theorem GradedMonoid.list_prod_map_eq_dProd (l : List Î±) (f : Î± â†’ GradedMonoid A) :
    (l.map f).prod = GradedMonoid.mk _ (l.dProd (fun i => (f i).1) fun i => (f i).2) := by
  rw [GradedMonoid.mk_list_dProd, GradedMonoid.mk]
  -- âŠ¢ List.prod (List.map f l) = List.prod (List.map (fun a => { fst := (f a).fst, â€¦
  simp_rw [Sigma.eta]
  -- ðŸŽ‰ no goals
#align graded_monoid.list_prod_map_eq_dprod GradedMonoid.list_prod_map_eq_dProd

theorem GradedMonoid.list_prod_ofFn_eq_dProd {n : â„•} (f : Fin n â†’ GradedMonoid A) :
    (List.ofFn f).prod =
      GradedMonoid.mk _ ((List.finRange n).dProd (fun i => (f i).1) fun i => (f i).2) :=
  by rw [List.ofFn_eq_map, GradedMonoid.list_prod_map_eq_dProd]
     -- ðŸŽ‰ no goals
#align graded_monoid.list_prod_of_fn_eq_dprod GradedMonoid.list_prod_ofFn_eq_dProd

end DProd

/-! ### Concrete instances -/


section

variable (Î¹) {R : Type*}

@[simps one]
instance One.gOne [Zero Î¹] [One R] : GradedMonoid.GOne fun _ : Î¹ => R where one := 1
#align has_one.ghas_one One.gOne

@[simps mul]
instance Mul.gMul [Add Î¹] [Mul R] : GradedMonoid.GMul fun _ : Î¹ => R where mul x y := x * y
#align has_mul.ghas_mul Mul.gMul

/-- If all grades are the same type and themselves form a monoid, then there is a trivial grading
structure. -/
@[simps gnpow]
instance Monoid.gMonoid [AddMonoid Î¹] [Monoid R] : GradedMonoid.GMonoid fun _ : Î¹ => R :=
  -- { Mul.gMul Î¹, One.gOne Î¹ with
  { One.gOne Î¹ with
    mul := fun x y => x * y
    one_mul := fun _ => Sigma.ext (zero_add _) (heq_of_eq (one_mul _))
    mul_one := fun _ => Sigma.ext (add_zero _) (heq_of_eq (mul_one _))
    mul_assoc := fun _ _ _ => Sigma.ext (add_assoc _ _ _) (heq_of_eq (mul_assoc _ _ _))
    gnpow := fun n _ a => a ^ n
    gnpow_zero' := fun _ => Sigma.ext (zero_nsmul _) (heq_of_eq (Monoid.npow_zero _))
    gnpow_succ' := fun _ âŸ¨_, _âŸ© => Sigma.ext (succ_nsmul _ _) (heq_of_eq (Monoid.npow_succ _ _)) }
#align monoid.gmonoid Monoid.gMonoid
#align monoid.gmonoid_gnpow Monoid.gMonoid_gnpow

/-- If all grades are the same type and themselves form a commutative monoid, then there is a
trivial grading structure. -/
instance CommMonoid.gCommMonoid [AddCommMonoid Î¹] [CommMonoid R] :
    GradedMonoid.GCommMonoid fun _ : Î¹ => R :=
  { Monoid.gMonoid Î¹ with
    mul_comm := fun _ _ => Sigma.ext (add_comm _ _) (heq_of_eq (mul_comm _ _)) }
#align comm_monoid.gcomm_monoid CommMonoid.gCommMonoid

/-- When all the indexed types are the same, the dependent product is just the regular product. -/
@[simp]
theorem List.dProd_monoid {Î±} [AddMonoid Î¹] [Monoid R] (l : List Î±) (fÎ¹ : Î± â†’ Î¹) (fA : Î± â†’ R) :
    @List.dProd _ _ (fun _ : Î¹ => R) _ _ l fÎ¹ fA = (l.map fA).prod := by
  match l with
  | [] =>
    rw [List.dProd_nil, List.map_nil, List.prod_nil]
    rfl
  | head::tail =>
    rw [List.dProd_cons, List.map_cons, List.prod_cons, List.dProd_monoid tail _ _]
    rfl
#align list.dprod_monoid List.dProd_monoid

end

/-! ### Shorthands for creating instance of the above typeclasses for collections of subobjects -/


section Subobjects

variable {R : Type*}

/-- A version of `GradedMonoid.GOne` for internally graded objects. -/
class SetLike.GradedOne {S : Type*} [SetLike S R] [One R] [Zero Î¹] (A : Î¹ â†’ S) : Prop where
  /-- One has grade zero -/
  one_mem : (1 : R) âˆˆ A 0
#align set_like.has_graded_one SetLike.GradedOne

theorem SetLike.one_mem_graded {S : Type*} [SetLike S R] [One R] [Zero Î¹] (A : Î¹ â†’ S)
    [SetLike.GradedOne A] : (1 : R) âˆˆ A 0 :=
  SetLike.GradedOne.one_mem
#align set_like.one_mem_graded SetLike.one_mem_graded

instance SetLike.gOne {S : Type*} [SetLike S R] [One R] [Zero Î¹] (A : Î¹ â†’ S)
    [SetLike.GradedOne A] : GradedMonoid.GOne fun i => A i
    where one := âŸ¨1, SetLike.one_mem_graded _âŸ©
#align set_like.ghas_one SetLike.gOne

@[simp]
theorem SetLike.coe_gOne {S : Type*} [SetLike S R] [One R] [Zero Î¹] (A : Î¹ â†’ S)
    [SetLike.GradedOne A] : â†‘(@GradedMonoid.GOne.one _ (fun i => A i) _ _) = (1 : R) :=
  rfl
#align set_like.coe_ghas_one SetLike.coe_gOne

/-- A version of `GradedMonoid.ghas_one` for internally graded objects. -/
class SetLike.GradedMul {S : Type*} [SetLike S R] [Mul R] [Add Î¹] (A : Î¹ â†’ S) : Prop where
  /-- Multiplication is homogeneous -/
  mul_mem : âˆ€ â¦ƒi jâ¦„ {gi gj}, gi âˆˆ A i â†’ gj âˆˆ A j â†’ gi * gj âˆˆ A (i + j)
#align set_like.has_graded_mul SetLike.GradedMul

theorem SetLike.mul_mem_graded {S : Type*} [SetLike S R] [Mul R] [Add Î¹] {A : Î¹ â†’ S}
    [SetLike.GradedMul A] â¦ƒi jâ¦„ {gi gj} (hi : gi âˆˆ A i) (hj : gj âˆˆ A j) : gi * gj âˆˆ A (i + j) :=
  SetLike.GradedMul.mul_mem hi hj
#align set_like.mul_mem_graded SetLike.mul_mem_graded

instance SetLike.gMul {S : Type*} [SetLike S R] [Mul R] [Add Î¹] (A : Î¹ â†’ S)
    [SetLike.GradedMul A] : GradedMonoid.GMul fun i => A i
    where mul := fun a b => âŸ¨(a * b : R), SetLike.mul_mem_graded a.prop b.propâŸ©
#align set_like.ghas_mul SetLike.gMul

/-
Porting note: simpNF linter returns

"Left-hand side does not simplify, when using the simp lemma on itself."

However, simp does indeed solve the following. Possibly related std#71,std#78

example {S : Type*} [SetLike S R] [Mul R] [Add Î¹] (A : Î¹ â†’ S)
    [SetLike.GradedMul A] {i j : Î¹} (x : A i) (y : A j) :
    â†‘(@GradedMonoid.GMul.mul _ (fun i => A i) _ _ _ _ x y) = (x * y : R) := by simp

-/
@[simp,nolint simpNF]
theorem SetLike.coe_gMul {S : Type*} [SetLike S R] [Mul R] [Add Î¹] (A : Î¹ â†’ S)
    [SetLike.GradedMul A] {i j : Î¹} (x : A i) (y : A j) :
    â†‘(@GradedMonoid.GMul.mul _ (fun i => A i) _ _ _ _ x y) = (x * y : R) :=
  rfl
#align set_like.coe_ghas_mul SetLike.coe_gMul


/-- A version of `GradedMonoid.GMonoid` for internally graded objects. -/
class SetLike.GradedMonoid {S : Type*} [SetLike S R] [Monoid R] [AddMonoid Î¹] (A : Î¹ â†’ S) extends
  SetLike.GradedOne A, SetLike.GradedMul A : Prop
#align set_like.graded_monoid SetLike.GradedMonoid

namespace SetLike

variable {S : Type*} [SetLike S R] [Monoid R] [AddMonoid Î¹]

variable {A : Î¹ â†’ S} [SetLike.GradedMonoid A]

theorem pow_mem_graded (n : â„•) {r : R} {i : Î¹} (h : r âˆˆ A i) : r ^ n âˆˆ A (n â€¢ i) := by
  match n with
  | 0 =>
    rw [pow_zero, zero_nsmul]
    exact one_mem_graded _
  | n+1 =>
    rw [pow_succ', succ_nsmul']
    exact mul_mem_graded (pow_mem_graded n h) h
#align set_like.pow_mem_graded SetLike.pow_mem_graded

theorem list_prod_map_mem_graded {Î¹'} (l : List Î¹') (i : Î¹' â†’ Î¹) (r : Î¹' â†’ R)
    (h : âˆ€ j âˆˆ l, r j âˆˆ A (i j)) : (l.map r).prod âˆˆ A (l.map i).sum := by
  match l with
  | [] =>
    rw [List.map_nil, List.map_nil, List.prod_nil, List.sum_nil]
    exact one_mem_graded _
  | head::tail =>
    rw [List.map_cons, List.map_cons, List.prod_cons, List.sum_cons]
    exact
      mul_mem_graded (h _ <| List.mem_cons_self _ _)
        (list_prod_map_mem_graded tail _ _ <| fun j hj => h _ <| List.mem_cons_of_mem _ hj)
#align set_like.list_prod_map_mem_graded SetLike.list_prod_map_mem_graded

theorem list_prod_ofFn_mem_graded {n} (i : Fin n â†’ Î¹) (r : Fin n â†’ R) (h : âˆ€ j, r j âˆˆ A (i j)) :
    (List.ofFn r).prod âˆˆ A (List.ofFn i).sum := by
  rw [List.ofFn_eq_map, List.ofFn_eq_map]
  -- âŠ¢ List.prod (List.map r (List.finRange n)) âˆˆ A (List.sum (List.map i (List.fin â€¦
  exact list_prod_map_mem_graded _ _ _ fun _ _ => h _
  -- ðŸŽ‰ no goals
#align set_like.list_prod_of_fn_mem_graded SetLike.list_prod_ofFn_mem_graded

end SetLike

/-- Build a `GMonoid` instance for a collection of subobjects. -/
instance SetLike.gMonoid {S : Type*} [SetLike S R] [Monoid R] [AddMonoid Î¹] (A : Î¹ â†’ S)
    [SetLike.GradedMonoid A] : GradedMonoid.GMonoid fun i => A i :=
  { SetLike.gOne A,
    SetLike.gMul A with
    one_mul := fun âŸ¨_, _, _âŸ© => Sigma.subtype_ext (zero_add _) (one_mul _)
    mul_one := fun âŸ¨_, _, _âŸ© => Sigma.subtype_ext (add_zero _) (mul_one _)
    mul_assoc := fun âŸ¨_, _, _âŸ© âŸ¨_, _, _âŸ© âŸ¨_, _, _âŸ© =>
      Sigma.subtype_ext (add_assoc _ _ _) (mul_assoc _ _ _)
    gnpow := fun n _ a => âŸ¨(a:R)^n, SetLike.pow_mem_graded n a.propâŸ©
    gnpow_zero' := fun _ => Sigma.subtype_ext (zero_nsmul _) (pow_zero _)
    gnpow_succ' := fun _ _ => Sigma.subtype_ext (succ_nsmul _ _) (pow_succ _ _) }
#align set_like.gmonoid SetLike.gMonoid

/-
Porting note: simpNF linter returns

"Left-hand side does not simplify, when using the simp lemma on itself."

However, simp does indeed solve the following. Possibly related std#71,std#78

example {S : Type*} [SetLike S R] [Monoid R] [AddMonoid Î¹] (A : Î¹ â†’ S)
    [SetLike.GradedMonoid A] {i : Î¹} (x : A i) (n : â„•) :
    â†‘(@GradedMonoid.GMonoid.gnpow _ (fun i => A i) _ _ n _ x) = (x:R)^n := by simp

-/
@[simp,nolint simpNF]
theorem SetLike.coe_gnpow {S : Type*} [SetLike S R] [Monoid R] [AddMonoid Î¹] (A : Î¹ â†’ S)
    [SetLike.GradedMonoid A] {i : Î¹} (x : A i) (n : â„•) :
    â†‘(@GradedMonoid.GMonoid.gnpow _ (fun i => A i) _ _ n _ x) = (x:R)^n :=
  rfl
#align set_like.coe_gnpow SetLike.coe_gnpow

/-- Build a `GCommMonoid` instance for a collection of subobjects. -/
instance SetLike.gCommMonoid {S : Type*} [SetLike S R] [CommMonoid R] [AddCommMonoid Î¹] (A : Î¹ â†’ S)
    [SetLike.GradedMonoid A] : GradedMonoid.GCommMonoid fun i => A i :=
  { SetLike.gMonoid A with
    mul_comm := fun âŸ¨_, _, _âŸ© âŸ¨_, _, _âŸ© => Sigma.subtype_ext (add_comm _ _) (mul_comm _ _) }
#align set_like.gcomm_monoid SetLike.gCommMonoid

section DProd

open SetLike SetLike.GradedMonoid

variable {Î± S : Type*} [SetLike S R] [Monoid R] [AddMonoid Î¹]

/-
Porting note: simpNF linter returns

"Left-hand side does not simplify, when using the simp lemma on itself."

However, simp does indeed solve the following. Possibly related std#71,std#78

example (A : Î¹ â†’ S) [SetLike.GradedMonoid A] (fÎ¹ : Î± â†’ Î¹)
    (fA : âˆ€ a, A (fÎ¹ a)) (l : List Î±) : â†‘(@List.dProd _ _ (fun i => â†¥(A i)) _ _ l fÎ¹ fA)
    = (List.prod (l.map fun a => fA a) : R) := by simp
-/
/-- Coercing a dependent product of subtypes is the same as taking the regular product of the
coercions. -/
@[simp,nolint simpNF]
theorem SetLike.coe_list_dProd (A : Î¹ â†’ S) [SetLike.GradedMonoid A] (fÎ¹ : Î± â†’ Î¹)
    (fA : âˆ€ a, A (fÎ¹ a)) (l : List Î±) : â†‘(@List.dProd _ _ (fun i => â†¥(A i)) _ _ l fÎ¹ fA)
    = (List.prod (l.map fun a => fA a) : R) := by
  match l with
  | [] =>
    rw [List.dProd_nil, coe_gOne, List.map_nil, List.prod_nil]
  | head::tail =>
    rw [List.dProd_cons, coe_gMul, List.map_cons, List.prod_cons,
      SetLike.coe_list_dProd _ _ _ tail]
#align set_like.coe_list_dprod SetLike.coe_list_dProd

/-- A version of `List.coe_dProd_set_like` with `Subtype.mk`. -/
theorem SetLike.list_dProd_eq (A : Î¹ â†’ S) [SetLike.GradedMonoid A] (fÎ¹ : Î± â†’ Î¹) (fA : âˆ€ a, A (fÎ¹ a))
    (l : List Î±) :
    (@List.dProd _ _ (fun i => â†¥(A i)) _ _ l fÎ¹ fA ) =
      âŸ¨List.prod (l.map fun a => fA a),
        (l.dProdIndex_eq_map_sum fÎ¹).symm â–¸
          list_prod_map_mem_graded l _ _ fun i _ => (fA i).propâŸ© :=
  Subtype.ext <| SetLike.coe_list_dProd _ _ _ _
#align set_like.list_dprod_eq SetLike.list_dProd_eq

end DProd

end Subobjects

section HomogeneousElements

variable {R S : Type*} [SetLike S R]

/-- An element `a : R` is said to be homogeneous if there is some `i : Î¹` such that `a âˆˆ A i`. -/
def SetLike.Homogeneous (A : Î¹ â†’ S) (a : R) : Prop :=
  âˆƒ i, a âˆˆ A i
#align set_like.is_homogeneous SetLike.Homogeneous

@[simp]
theorem SetLike.homogeneous_coe {A : Î¹ â†’ S} {i} (x : A i) : SetLike.Homogeneous A (x : R) :=
  âŸ¨i, x.propâŸ©
#align set_like.is_homogeneous_coe SetLike.homogeneous_coe

theorem SetLike.homogeneous_one [Zero Î¹] [One R] (A : Î¹ â†’ S) [SetLike.GradedOne A] :
    SetLike.Homogeneous A (1 : R) :=
  âŸ¨0, SetLike.one_mem_graded _âŸ©
#align set_like.is_homogeneous_one SetLike.homogeneous_one

theorem SetLike.homogeneous_mul [Add Î¹] [Mul R] {A : Î¹ â†’ S} [SetLike.GradedMul A] {a b : R} :
    SetLike.Homogeneous A a â†’ SetLike.Homogeneous A b â†’ SetLike.Homogeneous A (a * b)
  | âŸ¨i, hiâŸ©, âŸ¨j, hjâŸ© => âŸ¨i + j, SetLike.mul_mem_graded hi hjâŸ©
#align set_like.is_homogeneous.mul SetLike.homogeneous_mul

/-- When `A` is a `SetLike.GradedMonoid A`, then the homogeneous elements forms a submonoid. -/
def SetLike.homogeneousSubmonoid [AddMonoid Î¹] [Monoid R] (A : Î¹ â†’ S) [SetLike.GradedMonoid A] :
    Submonoid R where
  carrier := { a | SetLike.Homogeneous A a }
  one_mem' := SetLike.homogeneous_one A
  mul_mem' a b := SetLike.homogeneous_mul a b
#align set_like.homogeneous_submonoid SetLike.homogeneousSubmonoid

end HomogeneousElements
