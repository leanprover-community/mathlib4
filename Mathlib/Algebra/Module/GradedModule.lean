/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import Mathlib.RingTheory.GradedAlgebra.Basic
import Mathlib.Algebra.GradedMulAction
import Mathlib.Algebra.DirectSum.Decomposition
import Mathlib.Algebra.Module.BigOperators

/-!
# Graded Module

Given an `R`-algebra `A` graded by `ùìê`, a graded `A`-module `M` is expressed as
`DirectSum.Decomposition ùìú` and `SetLike.GradedSMul ùìê ùìú`.
Then `‚®Å i, ùìú i` is an `A`-module and is isomorphic to `M`.

## Tags

graded module
-/


section

open DirectSum

variable {ŒπA ŒπB : Type*} (A : ŒπA ‚Üí Type*) (M : ŒπB ‚Üí Type*)

namespace DirectSum

open GradedMonoid

/-- A graded version of `DistribMulAction`. -/
class GdistribMulAction [AddMonoid ŒπA] [VAdd ŒπA ŒπB] [GMonoid A] [‚àÄ i, AddMonoid (M i)]
    extends GMulAction A M where
  smul_add {i j} (a : A i) (b c : M j) : smul a (b + c) = smul a b + smul a c
  smul_zero {i j} (a : A i) : smul a (0 : M j) = 0

/-- A graded version of `Module`. -/
class Gmodule [AddMonoid ŒπA] [VAdd ŒπA ŒπB] [‚àÄ i, AddMonoid (A i)] [‚àÄ i, AddMonoid (M i)] [GMonoid A]
    extends GdistribMulAction A M where
  add_smul {i j} (a a' : A i) (b : M j) : smul (a + a') b = smul a b + smul a' b
  zero_smul {i j} (b : M j) : smul (0 : A i) b = 0

/-- A graded version of `Semiring.toModule`. -/
instance GSemiring.toGmodule [AddMonoid ŒπA] [‚àÄ i : ŒπA, AddCommMonoid (A i)]
    [h : GSemiring A] : Gmodule A A :=
  { GMonoid.toGMulAction A with
    smul_add := fun _ _ _ ‚Ü¶ h.mul_add _ _ _
    smul_zero := fun _ ‚Ü¶ h.mul_zero _
    add_smul := fun _ _ ‚Ü¶ h.add_mul _ _
    zero_smul := fun _ ‚Ü¶ h.zero_mul _ }

variable [AddMonoid ŒπA] [VAdd ŒπA ŒπB] [‚àÄ i : ŒπA, AddCommMonoid (A i)] [‚àÄ i, AddCommMonoid (M i)]

/-- The piecewise multiplication from the `Mul` instance, as a bundled homomorphism. -/
@[simps]
def gsmulHom [GMonoid A] [Gmodule A M] {i j} : A i ‚Üí+ M j ‚Üí+ M (i +·µ• j) where
  toFun a :=
    { toFun := fun b ‚Ü¶ GSMul.smul a b
      map_zero' := GdistribMulAction.smul_zero _
      map_add' := GdistribMulAction.smul_add _ }
  map_zero' := AddMonoidHom.ext fun a ‚Ü¶ Gmodule.zero_smul a
  map_add' _a‚ÇÅ _a‚ÇÇ := AddMonoidHom.ext fun _b ‚Ü¶ Gmodule.add_smul _ _ _

namespace Gmodule

/-- For graded monoid `A` and a graded module `M` over `A`. `Gmodule.smulAddMonoidHom` is the
`‚®Å·µ¢ A·µ¢`-scalar multiplication on `‚®Å·µ¢ M·µ¢` induced by `gsmul_hom`. -/
def smulAddMonoidHom [DecidableEq ŒπA] [DecidableEq ŒπB] [GMonoid A] [Gmodule A M] :
    (‚®Å i, A i) ‚Üí+ (‚®Å i, M i) ‚Üí+ ‚®Å i, M i :=
  toAddMonoid fun _i ‚Ü¶
    AddMonoidHom.flip <|
      toAddMonoid fun _j ‚Ü¶ AddMonoidHom.flip <| (of M _).compHom.comp <| gsmulHom A M

section

open GradedMonoid DirectSum Gmodule

instance [DecidableEq ŒπA] [DecidableEq ŒπB] [GMonoid A] [Gmodule A M] :
    SMul (‚®Å i, A i) (‚®Å i, M i) where
  smul x y := smulAddMonoidHom A M x y

@[simp]
theorem smul_def [DecidableEq ŒπA] [DecidableEq ŒπB] [GMonoid A] [Gmodule A M]
    (x : ‚®Å i, A i) (y : ‚®Å i, M i) :
    x ‚Ä¢ y = smulAddMonoidHom _ _ x y := rfl

@[simp]
theorem smulAddMonoidHom_apply_of_of [DecidableEq ŒπA] [DecidableEq ŒπB] [GMonoid A] [Gmodule A M]
    {i j} (x : A i) (y : M j) :
    smulAddMonoidHom A M (DirectSum.of A i x) (of M j y) = of M (i +·µ• j) (GSMul.smul x y) := by
  simp [smulAddMonoidHom]

theorem of_smul_of [DecidableEq ŒπA] [DecidableEq ŒπB] [GMonoid A] [Gmodule A M]
    {i j} (x : A i) (y : M j) :
    DirectSum.of A i x ‚Ä¢ of M j y = of M (i +·µ• j) (GSMul.smul x y) := by simp

open AddMonoidHom

-- Almost identical to the proof of `direct_sum.one_mul`
private theorem one_smul' [DecidableEq ŒπA] [DecidableEq ŒπB] [GMonoid A] [Gmodule A M]
    (x : ‚®Å i, M i) :
    (1 : ‚®Å i, A i) ‚Ä¢ x = x := by
  suffices smulAddMonoidHom A M 1 = AddMonoidHom.id (‚®Å i, M i) from DFunLike.congr_fun this x
  apply DirectSum.addHom_ext; intro i xi
  rw [show (1 : DirectSum ŒπA fun i ‚Ü¶ A i) = (of A 0) GOne.one by rfl]
  rw [smulAddMonoidHom_apply_of_of]
  exact DirectSum.of_eq_of_gradedMonoid_eq (one_smul (GradedMonoid A) <| GradedMonoid.mk i xi)

-- Almost identical to the proof of `direct_sum.mul_assoc`
private theorem mul_smul' [DecidableEq ŒπA] [DecidableEq ŒπB] [GSemiring A] [Gmodule A M]
    (a b : ‚®Å i, A i)
    (c : ‚®Å i, M i) : (a * b) ‚Ä¢ c = a ‚Ä¢ b ‚Ä¢ c := by
  suffices
    (-- `fun a b c ‚Ü¶ (a * b) ‚Ä¢ c` as a bundled hom
              smulAddMonoidHom
              A M).compHom.comp
        (DirectSum.mulHom A) =
      (AddMonoidHom.compHom AddMonoidHom.flipHom <|
          (smulAddMonoidHom A M).flip.compHom.comp <| smulAddMonoidHom A M).flip
    from-- `fun a b c ‚Ü¶ a ‚Ä¢ (b ‚Ä¢ c)` as a bundled hom
      DFunLike.congr_fun (DFunLike.congr_fun (DFunLike.congr_fun this a) b) c
  ext ai ax bi bx ci cx : 6
  dsimp only [coe_comp, Function.comp_apply, compHom_apply_apply, flip_apply, flipHom_apply]
  rw [smulAddMonoidHom_apply_of_of, smulAddMonoidHom_apply_of_of, DirectSum.mulHom_of_of,
    smulAddMonoidHom_apply_of_of]
  exact
    DirectSum.of_eq_of_gradedMonoid_eq
      (mul_smul (GradedMonoid.mk ai ax) (GradedMonoid.mk bi bx) (GradedMonoid.mk ci cx))

/-- The `Module` derived from `gmodule A M`. -/
instance module [DecidableEq ŒπA] [DecidableEq ŒπB] [GSemiring A] [Gmodule A M] :
    Module (‚®Å i, A i) (‚®Å i, M i) where
  smul := (¬∑ ‚Ä¢ ¬∑)
  one_smul := one_smul' _ _
  mul_smul := mul_smul' _ _
  smul_add r := (smulAddMonoidHom A M r).map_add
  smul_zero r := (smulAddMonoidHom A M r).map_zero
  add_smul r s x := by simp only [smul_def, map_add, AddMonoidHom.add_apply]
  zero_smul x := by simp only [smul_def, map_zero, AddMonoidHom.zero_apply]

end

end Gmodule

end DirectSum

end

open DirectSum

variable {ŒπA ŒπM R A M œÉ œÉ' : Type*}
variable [AddMonoid ŒπA] [AddAction ŒπA ŒπM] [CommSemiring R] [Semiring A] [Algebra R A]
variable (ùìê : ŒπA ‚Üí œÉ') [SetLike œÉ' A]
variable (ùìú : ŒπM ‚Üí œÉ)

namespace SetLike

instance gmulAction [AddMonoid M] [DistribMulAction A M] [SetLike œÉ M] [SetLike.GradedMonoid ùìê]
    [SetLike.GradedSMul ùìê ùìú] : GradedMonoid.GMulAction (fun i ‚Ü¶ ùìê i) fun i ‚Ü¶ ùìú i :=
  { SetLike.toGSMul ùìê ùìú with
    one_smul := fun ‚ü®_i, _m‚ü© ‚Ü¶ Sigma.subtype_ext (zero_vadd _ _) (one_smul _ _)
    mul_smul := fun ‚ü®_i, _a‚ü© ‚ü®_j, _a'‚ü© ‚ü®_k, _b‚ü© ‚Ü¶
      Sigma.subtype_ext (add_vadd _ _ _) (mul_smul _ _ _) }

instance gdistribMulAction [AddMonoid M] [DistribMulAction A M] [SetLike œÉ M]
    [AddSubmonoidClass œÉ M] [SetLike.GradedMonoid ùìê] [SetLike.GradedSMul ùìê ùìú] :
    DirectSum.GdistribMulAction (fun i ‚Ü¶ ùìê i) fun i ‚Ü¶ ùìú i :=
  { SetLike.gmulAction ùìê ùìú with
    smul_add := fun _a _b _c ‚Ü¶ Subtype.ext <| smul_add _ _ _
    smul_zero := fun _a ‚Ü¶ Subtype.ext <| smul_zero _ }

variable [AddCommMonoid M] [Module A M] [SetLike œÉ M] [AddSubmonoidClass œÉ' A]
  [AddSubmonoidClass œÉ M] [SetLike.GradedMonoid ùìê] [SetLike.GradedSMul ùìê ùìú]

/-- `[SetLike.GradedMonoid ùìê] [SetLike.GradedSMul ùìê ùìú]` is the internal version of graded
  module, the internal version can be translated into the external version `gmodule`. -/
instance gmodule : DirectSum.Gmodule (fun i ‚Ü¶ ùìê i) fun i ‚Ü¶ ùìú i :=
  { SetLike.gdistribMulAction ùìê ùìú with
    smul := fun x y ‚Ü¶ ‚ü®(x : A) ‚Ä¢ (y : M), SetLike.GradedSMul.smul_mem x.2 y.2‚ü©
    add_smul := fun _a _a' _b ‚Ü¶ Subtype.ext <| add_smul _ _ _
    zero_smul := fun _b ‚Ü¶ Subtype.ext <| zero_smul _ _ }

end SetLike

namespace GradedModule

variable [AddCommMonoid M] [Module A M] [SetLike œÉ M] [AddSubmonoidClass œÉ' A]
  [AddSubmonoidClass œÉ M] [SetLike.GradedMonoid ùìê] [SetLike.GradedSMul ùìê ùìú]

/-- The smul multiplication of `A` on `‚®Å i, ùìú i` from `(‚®Å i, ùìê i) ‚Üí+ (‚®Å i, ùìú i) ‚Üí+ ‚®Å i, ùìú i`
turns `‚®Å i, ùìú i` into an `A`-module
-/
def isModule [DecidableEq ŒπA] [DecidableEq ŒπM] [GradedRing ùìê] : Module A (‚®Å i, ùìú i) :=
  { Module.compHom _ (DirectSum.decomposeRingEquiv ùìê : A ‚âÉ+* ‚®Å i, ùìê i).toRingHom with
    smul := fun a b ‚Ü¶ DirectSum.decompose ùìê a ‚Ä¢ b }

/-- `‚®Å i, ùìú i` and `M` are isomorphic as `A`-modules.
"The internal version" and "the external version" are isomorphism as `A`-modules.
-/
def linearEquiv [DecidableEq ŒπA] [DecidableEq ŒπM] [GradedRing ùìê] [DirectSum.Decomposition ùìú] :
    @LinearEquiv A A _ _ (RingHom.id A) (RingHom.id A) _ _ M (‚®Å i, ùìú i) _
    _ _ (by letI := isModule ùìê ùìú; infer_instance) := by
  letI h := isModule ùìê ùìú
  refine ‚ü®‚ü®(DirectSum.decomposeAddEquiv ùìú).toAddHom, ?_‚ü©,
    (DirectSum.decomposeAddEquiv ùìú).symm.toFun, (DirectSum.decomposeAddEquiv ùìú).left_inv,
    (DirectSum.decomposeAddEquiv ùìú).right_inv‚ü©
  intro x y
  classical
  rw [AddHom.toFun_eq_coe, ‚Üê DirectSum.sum_support_decompose ùìê x, map_sum, Finset.sum_smul,
    AddEquiv.coe_toAddHom, map_sum, Finset.sum_smul]
  refine Finset.sum_congr rfl (fun i _hi ‚Ü¶ ?_)
  rw [RingHom.id_apply, ‚Üê DirectSum.sum_support_decompose ùìú y, map_sum, Finset.smul_sum, map_sum,
    Finset.smul_sum]
  refine Finset.sum_congr rfl (fun j _hj ‚Ü¶ ?_)
  rw [show (decompose ùìê x i : A) ‚Ä¢ (decomposeAddEquiv ùìú ‚Üë(decompose ùìú y j) : (‚®Å i, ùìú i)) =
    DirectSum.Gmodule.smulAddMonoidHom _ _ (decompose ùìê ‚Üë(decompose ùìê x i))
    (decomposeAddEquiv ùìú ‚Üë(decompose ùìú y j)) from DirectSum.Gmodule.smul_def _ _ _ _]
  simp only [decomposeAddEquiv_apply, decompose_coe, Gmodule.smulAddMonoidHom_apply_of_of]
  convert DirectSum.decompose_coe ùìú _
  rfl

end GradedModule
