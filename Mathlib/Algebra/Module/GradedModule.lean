/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import Mathlib.RingTheory.GradedAlgebra.Basic
import Mathlib.Algebra.GradedMulAction
import Mathlib.Algebra.DirectSum.Decomposition
import Mathlib.Algebra.Module.BigOperators
import Mathlib.Algebra.GradedMonoid

#align_import algebra.module.graded_module from "leanprover-community/mathlib"@"59cdeb0da2480abbc235b7e611ccd9a7e5603d7c"

/-!
# Graded Module

Given an `R`-algebra `A` graded by `ð“`, a graded `A`-module `M` is expressed as
`DirectSum.Decomposition ð“œ` and `SetLike.GradedSMul ð“ ð“œ`.
Then `â¨ i, ð“œ i` is an `A`-module and is isomorphic to `M`.

## Tags

graded module
-/


section

open DirectSum

variable {Î¹A Î¹B : Type*} (A : Î¹A â†’ Type*) (M : Î¹B â†’ Type*)

namespace DirectSum

open GradedMonoid

/-- A graded version of `DistribMulAction`. -/
class GdistribMulAction [AddMonoid Î¹A] [VAdd Î¹A Î¹B] [GMonoid A] [âˆ€ i, AddMonoid (M i)]
    extends GMulAction A M where
  smul_add {i j} (a : A i) (b c : M j) : smul a (b + c) = smul a b + smul a c
  smul_zero {i j} (a : A i) : smul a (0 : M j) = 0
#align direct_sum.gdistrib_mul_action DirectSum.GdistribMulAction

/-- A graded version of `Module`. -/
class Gmodule [AddMonoid Î¹A] [VAdd Î¹A Î¹B] [âˆ€ i, AddMonoid (A i)] [âˆ€ i, AddMonoid (M i)] [GMonoid A]
    extends GdistribMulAction A M where
  add_smul {i j} (a a' : A i) (b : M j) : smul (a + a') b = smul a b + smul a' b
  zero_smul {i j} (b : M j) : smul (0 : A i) b = 0
#align direct_sum.gmodule DirectSum.Gmodule

/-- A graded version of `Semiring.toModule`. -/
instance GSemiring.toGmodule [AddMonoid Î¹A] [âˆ€ i : Î¹A, AddCommMonoid (A i)]
    [h : GSemiring A] : Gmodule A A :=
  { GMonoid.toGMulAction A with
    smul_add := fun _ _ _ => h.mul_add _ _ _
    smul_zero := fun _ => h.mul_zero _
    add_smul := fun _ _ => h.add_mul _ _
    zero_smul := fun _ => h.zero_mul _ }
#align direct_sum.gsemiring.to_gmodule DirectSum.GSemiring.toGmodule

variable [AddMonoid Î¹A] [VAdd Î¹A Î¹B] [âˆ€ i : Î¹A, AddCommMonoid (A i)] [âˆ€ i, AddCommMonoid (M i)]

/-- The piecewise multiplication from the `Mul` instance, as a bundled homomorphism. -/
@[simps]
def gsmulHom [GMonoid A] [Gmodule A M] {i j} : A i â†’+ M j â†’+ M (i +áµ¥ j) where
  toFun a :=
    { toFun := fun b => GSMul.smul a b
      map_zero' := GdistribMulAction.smul_zero _
      map_add' := GdistribMulAction.smul_add _ }
  map_zero' := AddMonoidHom.ext fun a => Gmodule.zero_smul a
  map_add' _aâ‚ _aâ‚‚ := AddMonoidHom.ext fun _b => Gmodule.add_smul _ _ _
#align direct_sum.gsmul_hom DirectSum.gsmulHom

namespace Gmodule

/-- For graded monoid `A` and a graded module `M` over `A`. `Gmodule.smulAddMonoidHom` is the
`â¨áµ¢ Aáµ¢`-scalar multiplication on `â¨áµ¢ Máµ¢` induced by `gsmul_hom`. -/
def smulAddMonoidHom [DecidableEq Î¹A] [DecidableEq Î¹B] [GMonoid A] [Gmodule A M] :
    (â¨ i, A i) â†’+ (â¨ i, M i) â†’+ â¨ i, M i :=
  toAddMonoid fun _i =>
    AddMonoidHom.flip <|
      toAddMonoid fun _j => AddMonoidHom.flip <| (of M _).compHom.comp <| gsmulHom A M
#align direct_sum.gmodule.smul_add_monoid_hom DirectSum.Gmodule.smulAddMonoidHom

section

open GradedMonoid DirectSum Gmodule

instance [DecidableEq Î¹A] [DecidableEq Î¹B] [GMonoid A] [Gmodule A M] :
    SMul (â¨ i, A i) (â¨ i, M i) where
  smul x y := smulAddMonoidHom A M x y

@[simp]
theorem smul_def [DecidableEq Î¹A] [DecidableEq Î¹B] [GMonoid A] [Gmodule A M]
    (x : â¨ i, A i) (y : â¨ i, M i) :
    x â€¢ y = smulAddMonoidHom _ _ x y := rfl
#align direct_sum.gmodule.smul_def DirectSum.Gmodule.smul_def

@[simp]
theorem smulAddMonoidHom_apply_of_of [DecidableEq Î¹A] [DecidableEq Î¹B] [GMonoid A] [Gmodule A M]
    {i j} (x : A i) (y : M j) :
    smulAddMonoidHom A M (DirectSum.of A i x) (of M j y) = of M (i +áµ¥ j) (GSMul.smul x y) := by
  simp [smulAddMonoidHom]
#align direct_sum.gmodule.smul_add_monoid_hom_apply_of_of DirectSum.Gmodule.smulAddMonoidHom_apply_of_of

-- @[simp] -- Porting note: simpNF lint
theorem of_smul_of [DecidableEq Î¹A] [DecidableEq Î¹B] [GMonoid A] [Gmodule A M]
    {i j} (x : A i) (y : M j) :
    DirectSum.of A i x â€¢ of M j y = of M (i +áµ¥ j) (GSMul.smul x y) :=
  smulAddMonoidHom_apply_of_of _ _ _ _
#align direct_sum.gmodule.of_smul_of DirectSum.Gmodule.of_smul_of

open AddMonoidHom

-- Porting note: renamed to one_smul' since DirectSum.Gmodule.one_smul already exists
-- Almost identical to the proof of `direct_sum.one_mul`
private theorem one_smul' [DecidableEq Î¹A] [DecidableEq Î¹B] [GMonoid A] [Gmodule A M]
    (x : â¨ i, M i) :
    (1 : â¨ i, A i) â€¢ x = x := by
  suffices smulAddMonoidHom A M 1 = AddMonoidHom.id (â¨ i, M i) from FunLike.congr_fun this x
  apply DirectSum.addHom_ext; intro i xi
  rw [show (1 : DirectSum Î¹A fun i => A i) = (of A 0) GOne.one by rfl]
  rw [smulAddMonoidHom_apply_of_of]
  exact DirectSum.of_eq_of_gradedMonoid_eq (one_smul (GradedMonoid A) <| GradedMonoid.mk i xi)

-- Porting note: renamed to mul_smul' since DirectSum.Gmodule.mul_smul already exists
-- Almost identical to the proof of `direct_sum.mul_assoc`
private theorem mul_smul' [DecidableEq Î¹A] [DecidableEq Î¹B] [GSemiring A] [Gmodule A M]
    (a b : â¨ i, A i)
    (c : â¨ i, M i) : (a * b) â€¢ c = a â€¢ b â€¢ c := by
  suffices
    (-- `fun a b c â†¦ (a * b) â€¢ c` as a bundled hom
              smulAddMonoidHom
              A M).compHom.comp
        (DirectSum.mulHom A) =
      (AddMonoidHom.compHom AddMonoidHom.flipHom <|
          (smulAddMonoidHom A M).flip.compHom.comp <| smulAddMonoidHom A M).flip
    from-- `fun a b c â†¦ a â€¢ (b â€¢ c)` as a bundled hom
      FunLike.congr_fun (FunLike.congr_fun (FunLike.congr_fun this a) b) c
  ext ai ax bi bx ci cx : 6
  dsimp only [coe_comp, Function.comp_apply, compHom_apply_apply, flip_apply, flipHom_apply]
  rw [smulAddMonoidHom_apply_of_of, smulAddMonoidHom_apply_of_of, DirectSum.mulHom_of_of,
    smulAddMonoidHom_apply_of_of]
  exact
    DirectSum.of_eq_of_gradedMonoid_eq
      (mul_smul (GradedMonoid.mk ai ax) (GradedMonoid.mk bi bx) (GradedMonoid.mk ci cx))

/-- The `Module` derived from `gmodule A M`. -/
instance module [DecidableEq Î¹A] [DecidableEq Î¹B] [GSemiring A] [Gmodule A M] :
    Module (â¨ i, A i) (â¨ i, M i) where
  smul := (Â· â€¢ Â·)
  one_smul := one_smul' _ _
  mul_smul := mul_smul' _ _
  smul_add r := (smulAddMonoidHom A M r).map_add
  smul_zero r := (smulAddMonoidHom A M r).map_zero
  add_smul r s x := by simp only [smul_def, map_add, AddMonoidHom.add_apply]
  zero_smul x := by simp only [smul_def, map_zero, AddMonoidHom.zero_apply]
#align direct_sum.gmodule.module DirectSum.Gmodule.module

end

end Gmodule

end DirectSum

end

open DirectSum BigOperators

variable {Î¹A Î¹M R A M Ïƒ Ïƒ' : Type*}

variable [AddMonoid Î¹A] [AddAction Î¹A Î¹M] [CommSemiring R] [Semiring A] [Algebra R A]

variable (ð“ : Î¹A â†’ Ïƒ') [SetLike Ïƒ' A]

variable (ð“œ : Î¹M â†’ Ïƒ)

namespace SetLike

instance gmulAction [AddMonoid M] [DistribMulAction A M] [SetLike Ïƒ M] [SetLike.GradedMonoid ð“]
    [SetLike.GradedSMul ð“ ð“œ] : GradedMonoid.GMulAction (fun i => ð“ i) fun i => ð“œ i :=
  { SetLike.toGSMul ð“ ð“œ with
    one_smul := fun âŸ¨_i, _mâŸ© => Sigma.subtype_ext (zero_vadd _ _) (one_smul _ _)
    mul_smul := fun âŸ¨_i, _aâŸ© âŸ¨_j, _a'âŸ© âŸ¨_k, _bâŸ© =>
      Sigma.subtype_ext (add_vadd _ _ _) (mul_smul _ _ _) }
#align set_like.gmul_action SetLike.gmulAction

instance gdistribMulAction [AddMonoid M] [DistribMulAction A M] [SetLike Ïƒ M]
    [AddSubmonoidClass Ïƒ M] [SetLike.GradedMonoid ð“] [SetLike.GradedSMul ð“ ð“œ] :
    DirectSum.GdistribMulAction (fun i => ð“ i) fun i => ð“œ i :=
  { SetLike.gmulAction ð“ ð“œ with
    smul_add := fun _a _b _c => Subtype.ext <| smul_add _ _ _
    smul_zero := fun _a => Subtype.ext <| smul_zero _ }
#align set_like.gdistrib_mul_action SetLike.gdistribMulAction

variable [AddCommMonoid M] [Module A M] [SetLike Ïƒ M] [AddSubmonoidClass Ïƒ' A]
  [AddSubmonoidClass Ïƒ M] [SetLike.GradedMonoid ð“] [SetLike.GradedSMul ð“ ð“œ]

/-- `[SetLike.GradedMonoid ð“] [SetLike.GradedSMul ð“ ð“œ]` is the internal version of graded
  module, the internal version can be translated into the external version `gmodule`. -/
instance gmodule : DirectSum.Gmodule (fun i => ð“ i) fun i => ð“œ i :=
  { SetLike.gdistribMulAction ð“ ð“œ with
    smul := fun x y => âŸ¨(x : A) â€¢ (y : M), SetLike.GradedSMul.smul_mem x.2 y.2âŸ©
    add_smul := fun _a _a' _b => Subtype.ext <| add_smul _ _ _
    zero_smul := fun _b => Subtype.ext <| zero_smul _ _ }
#align set_like.gmodule SetLike.gmodule

end SetLike

namespace GradedModule

variable [AddCommMonoid M] [Module A M] [SetLike Ïƒ M] [AddSubmonoidClass Ïƒ' A]
  [AddSubmonoidClass Ïƒ M] [SetLike.GradedMonoid ð“] [SetLike.GradedSMul ð“ ð“œ]

set_option maxHeartbeats 300000 in -- Porting note: needs more Heartbeats to elaborate
/-- The smul multiplication of `A` on `â¨ i, ð“œ i` from `(â¨ i, ð“ i) â†’+ (â¨ i, ð“œ i) â†’+ â¨ i, ð“œ i`
turns `â¨ i, ð“œ i` into an `A`-module
-/
def isModule [DecidableEq Î¹A] [DecidableEq Î¹M] [GradedRing ð“] : Module A (â¨ i, ð“œ i) :=
  { Module.compHom _ (DirectSum.decomposeRingEquiv ð“ : A â‰ƒ+* â¨ i, ð“ i).toRingHom with
    smul := fun a b => DirectSum.decompose ð“ a â€¢ b }
#align graded_module.is_module GradedModule.isModule

/-- `â¨ i, ð“œ i` and `M` are isomorphic as `A`-modules.
"The internal version" and "the external version" are isomorphism as `A`-modules.
-/
def linearEquiv [DecidableEq Î¹A] [DecidableEq Î¹M] [GradedRing ð“] [DirectSum.Decomposition ð“œ] :
    @LinearEquiv A A _ _ (RingHom.id A) (RingHom.id A) _ _ M (â¨ i, ð“œ i) _
    _ _ (by letI := isModule ð“ ð“œ; infer_instance) := by
  letI h := isModule ð“ ð“œ
  refine âŸ¨âŸ¨(DirectSum.decomposeAddEquiv ð“œ).toAddHom, ?_âŸ©,
    (DirectSum.decomposeAddEquiv ð“œ).symm.toFun, (DirectSum.decomposeAddEquiv ð“œ).left_inv,
    (DirectSum.decomposeAddEquiv ð“œ).right_invâŸ©
  intro x y
  classical
  rw [AddHom.toFun_eq_coe, â† DirectSum.sum_support_decompose ð“ x, map_sum, Finset.sum_smul,
    AddEquiv.coe_toAddHom, map_sum, Finset.sum_smul]
  refine Finset.sum_congr rfl (fun i _hi => ?_)
  rw [RingHom.id_apply, â† DirectSum.sum_support_decompose ð“œ y, map_sum, Finset.smul_sum, map_sum,
    Finset.smul_sum]
  refine Finset.sum_congr rfl (fun j _hj => ?_)
  rw [show (decompose ð“ x i : A) â€¢ (decomposeAddEquiv ð“œ â†‘(decompose ð“œ y j) : (â¨ i, ð“œ i)) =
    DirectSum.Gmodule.smulAddMonoidHom _ _ (decompose ð“ â†‘(decompose ð“ x i))
    (decomposeAddEquiv ð“œ â†‘(decompose ð“œ y j)) from DirectSum.Gmodule.smul_def _ _ _ _]
  simp only [decomposeAddEquiv_apply, Equiv.invFun_as_coe, Equiv.symm_symm, decompose_coe,
    Gmodule.smulAddMonoidHom_apply_of_of]
  convert DirectSum.decompose_coe ð“œ _
  rfl
#align graded_module.linear_equiv GradedModule.linearEquiv

/--
The projection map of an internally graded module.
-/
@[simps]
def proj [DecidableEq Î¹M] [Decomposition ð“œ]  (i : Î¹M) : M â†’+ M where
  toFun m := decompose ð“œ m i
  map_zero' := by simp
  map_add' := by simp

/--
For each `a : M`, `GradedModule.homogeneousComponents â„³ a` is the collection of the
homogeneous components of `a`, which is a finite subset of `M`.
-/
def homogeneousComponents [DecidableEq Î¹M] [Decomposition ð“œ] [DecidableEq M] (a : M) : Finset M :=
    (decompose ð“œ a).support.image (decompose ð“œ a Â·)

lemma homogeneous_of_mem_homogeneousComponents [DecidableEq Î¹M] [Decomposition ð“œ] [DecidableEq M]
    {a b : M} (hb : b âˆˆ homogeneousComponents ð“œ a) : SetLike.Homogeneous ð“œ b := by
  change b âˆˆ (decompose ð“œ a).support.image _ at hb
  aesop

section same_indexing_set

variable {ÏƒA ÏƒM : Type*} (ð’œ : â„• â†’ ÏƒA) (â„³ : â„• â†’ ÏƒM)
variable [AddCommMonoid M] [Module A M] [SetLike ÏƒA A] [SetLike ÏƒM M]
variable [AddSubmonoidClass ÏƒA A] [AddSubmonoidClass ÏƒM M]
variable [GradedRing ð’œ] [DirectSum.Decomposition â„³] [SetLike.GradedSMul ð’œ â„³]


lemma proj_smul_mem_right {i j : â„•} (a : A) (m : M) (hm : m âˆˆ â„³ i) :
    GradedModule.proj â„³ j (a â€¢ m) =
    if i â‰¤ j
    then GradedRing.proj ð’œ (j - i) a â€¢ m
    else 0 := by
  classical
  rw [â† DirectSum.sum_support_decompose â„³ (a â€¢ m), map_sum, Finset.sum_eq_single j,
    proj_apply, decompose_of_mem_same (hx := SetLike.coe_mem _)]
  pick_goal 2
  Â· intro n _ hne; rw [proj_apply, decompose_of_mem_ne (hx := SetLike.coe_mem _) hne]
  pick_goal 2
  Â· intro hj; simpa using hj
  letI := isModule ð’œ â„³

  have eq0 : decompose â„³ (a â€¢ m) = a â€¢ decompose â„³ m := (linearEquiv ð’œ â„³).1.map_smul a m
  rw [eq0]
  show ((DirectSum.decompose ð’œ a â€¢ DirectSum.decompose â„³ m) j : M) = _
  conv_lhs => rw [â† DirectSum.sum_support_decompose â„³ m,
    â† DirectSum.sum_support_decompose ð’œ a, DirectSum.decompose_sum,
    Finset.sum_smul, DirectSum.decompose_sum]
  simp_rw [Finset.smul_sum]
  have eq1 (k : â„•) :
    âˆ‘ j in (decompose â„³ m).support,
      (decompose ð’œ (decompose ð’œ a k)) â€¢ decompose â„³ (decompose â„³ m j) =
    decompose ð’œ (decompose ð’œ a k) â€¢ decompose â„³ m
  Â· rw [Finset.sum_eq_single i, decompose_of_mem_same â„³ hm]
    Â· intro j _ hne
      rw [decompose_of_mem_ne â„³ hm hne.symm, decompose_zero, smul_zero]
    Â· intro hi
      simp only [DFinsupp.mem_support_toFun, ne_eq, not_not] at hi
      simp only [decompose_coe, hi, ZeroMemClass.coe_zero, decompose_zero, smul_zero]
  simp_rw [eq1]
  lift m to â„³ i using hm
  simp_rw [decompose_coe, DirectSum.Gmodule.of_smul_of, vadd_eq_add]

  split_ifs with h
  Â· rw [DFinsupp.finset_sum_apply, Finset.sum_eq_single (j - i), DirectSum.coe_of_apply,
      if_pos (Nat.sub_add_cancel h)]
    Â· rfl
    Â· intro n _ hn2
      rw [of_eq_of_ne]
      contrapose! hn2
      exact Nat.sub_eq_of_eq_add hn2.symm |>.symm

    Â· intro H
      ext
      simp only [DFinsupp.mem_support_toFun, ne_eq, not_not] at H
      rw [H, Gmodule.zero_smul, coe_of_apply, if_pos (Nat.sub_add_cancel h)]
      rfl

  Â· rw [DFinsupp.finset_sum_apply]
    push_cast
    simp_rw [coe_of_apply]
    apply Finset.sum_eq_zero
    intro k _
    simp only [not_le] at h
    rw [if_neg]
    Â· rfl
    Â· linarith

end same_indexing_set

end GradedModule
