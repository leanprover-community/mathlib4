/-
Copyright (c) 2022 Pierre-Alexandre Bazin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Pierre-Alexandre Bazin
-/
import Mathlib.Algebra.Module.DedekindDomain
import Mathlib.LinearAlgebra.FreeModule.PID
import Mathlib.Algebra.Module.Projective
import Mathlib.Algebra.Category.ModuleCat.Biproducts

#align_import algebra.module.pid from "leanprover-community/mathlib"@"cdc34484a07418af43daf8198beaf5c00324bca8"

/-!
# Structure of finitely generated modules over a PID

## Main statements

* `Module.equiv_directSum_of_isTorsion` : A finitely generated torsion module over a PID is
  isomorphic to a direct sum of some `R ‚ß∏ R ‚àô (p i ^ e i)` where the `p i ^ e i` are prime powers.
* `Module.equiv_free_prod_directSum` : A finitely generated module over a PID is isomorphic to the
  product of a free module (its torsion free part) and a direct sum of the form above (its torsion
  submodule).

## Notation

* `R` is a PID and `M` is a (finitely generated for main statements) `R`-module, with additional
  torsion hypotheses in the intermediate lemmas.
* `N` is an `R`-module lying over a higher type universe than `R`. This assumption is needed on the
  final statement for technical reasons.
* `p` is an irreducible element of `R` or a tuple of these.

## Implementation details

We first prove (`Submodule.isInternal_prime_power_torsion_of_pid`) that a finitely generated
torsion module is the internal direct sum of its `p i ^ e i`-torsion submodules for some
(finitely many) prime powers `p i ^ e i`. This is proved in more generality for a Dedekind domain
at `Submodule.isInternal_prime_power_torsion`.

Then we treat the case of a `p ^ ‚àû`-torsion module (that is, a module where all elements are
cancelled by scalar multiplication by some power of `p`) and apply it to the `p i ^ e i`-torsion
submodules (that are `p i ^ ‚àû`-torsion) to get the result for torsion modules.

Then we get the general result using that a torsion free module is free (which has been proved at
`Module.free_of_finite_type_torsion_free'` at `LinearAlgebra.FreeModule.PID`.)

## Tags

Finitely generated module, principal ideal domain, classification, structure theorem
-/


universe u v

open scoped BigOperators Classical

variable {R : Type u} [CommRing R] [IsDomain R] [IsPrincipalIdealRing R]

variable {M : Type v} [AddCommGroup M] [Module R M]

variable {N : Type max u v} [AddCommGroup N] [Module R N]

open scoped DirectSum

open Submodule

open UniqueFactorizationMonoid

/-- A finitely generated torsion module over a PID is an internal direct sum of its
`p i ^ e i`-torsion submodules for some primes `p i` and numbers `e i`.-/
theorem Submodule.isInternal_prime_power_torsion_of_pid [Module.Finite R M]
    (hM : Module.IsTorsion R M) :
    DirectSum.IsInternal fun p : (factors (‚ä§ : Submodule R M).annihilator).toFinset =>
      torsionBy R M
        (IsPrincipal.generator (p : Ideal R) ^
          (factors (‚ä§ : Submodule R M).annihilator).count ‚Üëp) := by
  convert isInternal_prime_power_torsion hM
  -- ‚ä¢ torsionBy R M (IsPrincipal.generator ‚Üëx‚úù ^ Multiset.count (‚Üëx‚úù) (factors (an ‚Ä¶
  ext p : 1
  -- ‚ä¢ p ‚àà torsionBy R M (IsPrincipal.generator ‚Üëx‚úù ^ Multiset.count (‚Üëx‚úù) (factors ‚Ä¶
  rw [‚Üê torsionBySet_span_singleton_eq, Ideal.submodule_span_eq, ‚Üê Ideal.span_singleton_pow,
    Ideal.span_singleton_generator]
#align submodule.is_internal_prime_power_torsion_of_pid Submodule.isInternal_prime_power_torsion_of_pid

/-- A finitely generated torsion module over a PID is an internal direct sum of its
`p i ^ e i`-torsion submodules for some primes `p i` and numbers `e i`.-/
theorem Submodule.exists_isInternal_prime_power_torsion_of_pid [Module.Finite R M]
    (hM : Module.IsTorsion R M) :
    ‚àÉ (Œπ : Type u) (_ : Fintype Œπ) (_ : DecidableEq Œπ) (p : Œπ ‚Üí R) (_ : ‚àÄ i, Irreducible <| p i)
        (e : Œπ ‚Üí ‚Ñï), DirectSum.IsInternal fun i => torsionBy R M <| p i ^ e i := by
  refine' ‚ü®_, _, _, _, _, _, Submodule.isInternal_prime_power_torsion_of_pid hM‚ü©
  -- ‚ä¢ Fintype { x // x ‚àà Multiset.toFinset (factors (annihilator ‚ä§)) }
  exact Finset.fintypeCoeSort _
  -- ‚ä¢ ‚àÄ (i : { x // x ‚àà Multiset.toFinset (factors (annihilator ‚ä§)) }), Irreducibl ‚Ä¶
  ¬∑ rintro ‚ü®p, hp‚ü©
    -- ‚ä¢ Irreducible (IsPrincipal.generator ‚Üë{ val := p, property := hp })
    have hP := prime_of_factor p (Multiset.mem_toFinset.mp hp)
    -- ‚ä¢ Irreducible (IsPrincipal.generator ‚Üë{ val := p, property := hp })
    haveI := Ideal.isPrime_of_prime hP
    -- ‚ä¢ Irreducible (IsPrincipal.generator ‚Üë{ val := p, property := hp })
    exact (IsPrincipal.prime_generator_of_isPrime p hP.ne_zero).irreducible
    -- üéâ no goals
#align submodule.exists_is_internal_prime_power_torsion_of_pid Submodule.exists_isInternal_prime_power_torsion_of_pid

namespace Module

section PTorsion

variable {p : R} (hp : Irreducible p) (hM : Module.IsTorsion' M (Submonoid.powers p))

variable [dec : ‚àÄ x : M, Decidable (x = 0)]

open Ideal Submodule.IsPrincipal

theorem _root_.Ideal.torsionOf_eq_span_pow_pOrder (x : M) :
    torsionOf R M x = span {p ^ pOrder hM x} := by
  dsimp only [pOrder]
  -- ‚ä¢ torsionOf R M x = Ideal.span {p ^ Nat.find (_ : ‚àÉ n, p ^ n ‚Ä¢ x = 0)}
  rw [‚Üê (torsionOf R M x).span_singleton_generator, Ideal.span_singleton_eq_span_singleton, ‚Üê
    Associates.mk_eq_mk_iff_associated, Associates.mk_pow]
  have prop :
    (fun n : ‚Ñï => p ^ n ‚Ä¢ x = 0) = fun n : ‚Ñï =>
      (Associates.mk <| generator <| torsionOf R M x) ‚à£ Associates.mk p ^ n :=
    by ext n; rw [‚Üê Associates.mk_pow, Associates.mk_dvd_mk, ‚Üê mem_iff_generator_dvd]; rfl
  have := (isTorsion'_powers_iff p).mp hM x; rw [prop] at this
  -- ‚ä¢ Associates.mk (generator (torsionOf R M x)) = Associates.mk p ^ Nat.find (_  ‚Ä¶
                                             -- ‚ä¢ Associates.mk (generator (torsionOf R M x)) = Associates.mk p ^ Nat.find (_  ‚Ä¶
  classical
  convert
    Associates.eq_pow_find_of_dvd_irreducible_pow ((Associates.irreducible_mk p).mpr hp)
      this.choose_spec
#align ideal.torsion_of_eq_span_pow_p_order Ideal.torsionOf_eq_span_pow_pOrder

theorem p_pow_smul_lift {x y : M} {k : ‚Ñï} (hM' : Module.IsTorsionBy R M (p ^ pOrder hM y))
    (h : p ^ k ‚Ä¢ x ‚àà R ‚àô y) : ‚àÉ a : R, p ^ k ‚Ä¢ x = p ^ k ‚Ä¢ a ‚Ä¢ y := by
  -- Porting note: needed to make `smul_smul` work below.
  letI : MulAction R M := MulActionWithZero.toMulAction
  -- ‚ä¢ ‚àÉ a, p ^ k ‚Ä¢ x = p ^ k ‚Ä¢ a ‚Ä¢ y
  by_cases hk : k ‚â§ pOrder hM y
  -- ‚ä¢ ‚àÉ a, p ^ k ‚Ä¢ x = p ^ k ‚Ä¢ a ‚Ä¢ y
  ¬∑ let f :=
      ((R ‚àô p ^ (pOrder hM y - k) * p ^ k).quotEquivOfEq _ ?_).trans
        (quotTorsionOfEquivSpanSingleton R M y)
    have : f.symm ‚ü®p ^ k ‚Ä¢ x, h‚ü© ‚àà
        R ‚àô Ideal.Quotient.mk (R ‚àô p ^ (pOrder hM y - k) * p ^ k) (p ^ k) := by
      rw [‚Üê Quotient.torsionBy_eq_span_singleton, mem_torsionBy_iff, ‚Üê f.symm.map_smul]
      convert f.symm.map_zero; ext
      rw [coe_smul_of_tower, coe_mk, coe_zero, smul_smul, ‚Üê pow_add, Nat.sub_add_cancel hk, @hM' x]
      ¬∑ exact mem_nonZeroDivisors_of_ne_zero (pow_ne_zero _ hp.ne_zero)
    rw [Submodule.mem_span_singleton] at this; obtain ‚ü®a, ha‚ü© := this; use a
    -- ‚ä¢ ‚àÉ a, p ^ k ‚Ä¢ x = p ^ k ‚Ä¢ a ‚Ä¢ y
                                               -- ‚ä¢ ‚àÉ a, p ^ k ‚Ä¢ x = p ^ k ‚Ä¢ a ‚Ä¢ y
                                                                       -- ‚ä¢ p ^ k ‚Ä¢ x = p ^ k ‚Ä¢ a ‚Ä¢ y
    rw [f.eq_symm_apply, ‚Üê Ideal.Quotient.mk_eq_mk, ‚Üê Quotient.mk_smul] at ha
    -- ‚ä¢ p ^ k ‚Ä¢ x = p ^ k ‚Ä¢ a ‚Ä¢ y
    dsimp only [smul_eq_mul, LinearEquiv.trans_apply, Submodule.quotEquivOfEq_mk,
      quotTorsionOfEquivSpanSingleton_apply_mk] at ha
    rw [smul_smul, mul_comm]; exact congr_arg ((‚Üë) : _ ‚Üí M) ha.symm
    -- ‚ä¢ p ^ k ‚Ä¢ x = (a * p ^ k) ‚Ä¢ y
                              -- ‚ä¢ Submodule.span R {p ^ (pOrder hM y - k) * p ^ k} = torsionOf R M y
    ¬∑ symm; convert Ideal.torsionOf_eq_span_pow_pOrder hp hM y
      -- ‚ä¢ torsionOf R M y = Submodule.span R {p ^ (pOrder hM y - k) * p ^ k}
            -- ‚ä¢ p ^ (pOrder hM y - k) * p ^ k = p ^ pOrder hM y
      rw [‚Üê pow_add, Nat.sub_add_cancel hk]
      -- üéâ no goals
  ¬∑ use 0
    -- ‚ä¢ p ^ k ‚Ä¢ x = p ^ k ‚Ä¢ 0 ‚Ä¢ y
    rw [zero_smul, smul_zero, ‚Üê Nat.sub_add_cancel (le_of_not_le hk), pow_add, mul_smul, hM',
      smul_zero]
#align module.p_pow_smul_lift Module.p_pow_smul_lift

open Submodule.Quotient

theorem exists_smul_eq_zero_and_mk_eq {z : M} (hz : Module.IsTorsionBy R M (p ^ pOrder hM z))
    {k : ‚Ñï} (f : (R ‚ß∏ R ‚àô p ^ k) ‚Üí‚Çó[R] M ‚ß∏ R ‚àô z) :
    ‚àÉ x : M, p ^ k ‚Ä¢ x = 0 ‚àß Submodule.Quotient.mk (p := span R {z}) x = f 1 := by
  have f1 := mk_surjective (R ‚àô z) (f 1)
  -- ‚ä¢ ‚àÉ x, p ^ k ‚Ä¢ x = 0 ‚àß Submodule.Quotient.mk x = ‚Üëf 1
  have : p ^ k ‚Ä¢ f1.choose ‚àà R ‚àô z := by
    rw [‚Üê Quotient.mk_eq_zero, mk_smul, f1.choose_spec, ‚Üê f.map_smul]
    convert f.map_zero; change _ ‚Ä¢ Submodule.Quotient.mk _ = _
    rw [‚Üê mk_smul, Quotient.mk_eq_zero, Algebra.id.smul_eq_mul, mul_one]
    exact Submodule.mem_span_singleton_self _
  obtain ‚ü®a, ha‚ü© := p_pow_smul_lift hp hM hz this
  -- ‚ä¢ ‚àÉ x, p ^ k ‚Ä¢ x = 0 ‚àß Submodule.Quotient.mk x = ‚Üëf 1
  refine' ‚ü®f1.choose - a ‚Ä¢ z, by rw [smul_sub, sub_eq_zero, ha], _‚ü©
  -- ‚ä¢ Submodule.Quotient.mk (Exists.choose f1 - a ‚Ä¢ z) = ‚Üëf 1
  rw [mk_sub, mk_smul, (Quotient.mk_eq_zero _).mpr <| Submodule.mem_span_singleton_self _,
    smul_zero, sub_zero, f1.choose_spec]
#align module.exists_smul_eq_zero_and_mk_eq Module.exists_smul_eq_zero_and_mk_eq

open Finset Multiset

set_option maxHeartbeats 800000 in
/-- A finitely generated `p ^ ‚àû`-torsion module over a PID is isomorphic to a direct sum of some
  `R ‚ß∏ R ‚àô (p ^ e i)` for some `e i`.-/
theorem torsion_by_prime_power_decomposition (hN : Module.IsTorsion' N (Submonoid.powers p))
    [h' : Module.Finite R N] :
    ‚àÉ (d : ‚Ñï) (k : Fin d ‚Üí ‚Ñï), Nonempty <| N ‚âÉ‚Çó[R] ‚®Å i : Fin d, R ‚ß∏ R ‚àô p ^ (k i : ‚Ñï) := by
  obtain ‚ü®d, s, hs‚ü© := @Module.Finite.exists_fin _ _ _ _ _ h'; use d; clear h'
  -- ‚ä¢ ‚àÉ d k, Nonempty (N ‚âÉ‚Çó[R] ‚®Å (i : Fin d), R ‚ß∏ Submodule.span R {p ^ k i})
                                                               -- ‚ä¢ ‚àÉ k, Nonempty (N ‚âÉ‚Çó[R] ‚®Å (i : Fin d), R ‚ß∏ Submodule.span R {p ^ k i})
                                                                      -- ‚ä¢ ‚àÉ k, Nonempty (N ‚âÉ‚Çó[R] ‚®Å (i : Fin d), R ‚ß∏ Submodule.span R {p ^ k i})
  induction' d with d IH generalizing N
  -- ‚ä¢ ‚àÉ k, Nonempty (N ‚âÉ‚Çó[R] ‚®Å (i : Fin Nat.zero), R ‚ß∏ Submodule.span R {p ^ k i})
  ¬∑ simp only [Nat.zero_eq] at *
    -- ‚ä¢ ‚àÉ k, Nonempty (N ‚âÉ‚Çó[R] ‚®Å (i : Fin Nat.zero), R ‚ß∏ Submodule.span R {p ^ k i})
    -- Porting note: was `use fun i => finZeroElim i`
    use finZeroElim
    -- ‚ä¢ Nonempty (N ‚âÉ‚Çó[R] ‚®Å (i : Fin Nat.zero), R ‚ß∏ Submodule.span R {p ^ finZeroEli ‚Ä¶
    rw [Set.range_eq_empty, Submodule.span_empty] at hs
    -- ‚ä¢ Nonempty (N ‚âÉ‚Çó[R] ‚®Å (i : Fin Nat.zero), R ‚ß∏ Submodule.span R {p ^ finZeroEli ‚Ä¶
    haveI : Unique N :=
      ‚ü®‚ü®0‚ü©, fun x => by dsimp; rw [‚Üê Submodule.mem_bot R, hs]; exact Submodule.mem_top‚ü©
    haveI : IsEmpty (Fin Nat.zero) := inferInstanceAs (IsEmpty (Fin 0))
    -- ‚ä¢ Nonempty (N ‚âÉ‚Çó[R] ‚®Å (i : Fin Nat.zero), R ‚ß∏ Submodule.span R {p ^ finZeroEli ‚Ä¶
    exact ‚ü®0‚ü©
    -- üéâ no goals
  ¬∑ have : ‚àÄ x : N, Decidable (x = 0) := fun _ => by classical infer_instance
    -- ‚ä¢ ‚àÉ k, Nonempty (N ‚âÉ‚Çó[R] ‚®Å (i : Fin (Nat.succ d)), R ‚ß∏ Submodule.span R {p ^ k ‚Ä¶
    obtain ‚ü®j, hj‚ü© := exists_isTorsionBy hN d.succ d.succ_ne_zero s hs
    -- ‚ä¢ ‚àÉ k, Nonempty (N ‚âÉ‚Çó[R] ‚®Å (i : Fin (Nat.succ d)), R ‚ß∏ Submodule.span R {p ^ k ‚Ä¶
    let s' : Fin d ‚Üí N ‚ß∏ R ‚àô s j := Submodule.Quotient.mk ‚àò s ‚àò j.succAbove
    -- ‚ä¢ ‚àÉ k, Nonempty (N ‚âÉ‚Çó[R] ‚®Å (i : Fin (Nat.succ d)), R ‚ß∏ Submodule.span R {p ^ k ‚Ä¶
    -- Porting note(https://github.com/leanprover-community/mathlib4/issues/5732):
    -- `obtain` doesn't work with placeholders.
    have := IH ?_ s' ?_
    obtain ‚ü®k, ‚ü®f‚ü©‚ü© := this
    clear IH
    ¬∑ have : ‚àÄ i : Fin d,
          ‚àÉ x : N, p ^ k i ‚Ä¢ x = 0 ‚àß f (Submodule.Quotient.mk x) = DirectSum.lof R _ _ i 1 := by
        intro i
        let fi := f.symm.toLinearMap.comp (DirectSum.lof _ _ _ i)
        obtain ‚ü®x, h0, h1‚ü© := exists_smul_eq_zero_and_mk_eq hp hN hj fi; refine' ‚ü®x, h0, _‚ü©; rw [h1]
        simp only [LinearMap.coe_comp, f.symm.coe_toLinearMap, f.apply_symm_apply,
          Function.comp_apply]
      refine ‚ü®?_, ‚ü®?_‚ü©‚ü©
      -- ‚ä¢ Fin (Nat.succ d) ‚Üí ‚Ñï
      ¬∑ exact fun a => (fun i => (Option.rec (pOrder hN (s j)) k i : ‚Ñï)) (finSuccEquiv d a)
        -- üéâ no goals
      ¬∑ refine (((@lequivProdOfRightSplitExact _ _ _ _ _ _ _ _ _ _ _ _
          ((f.trans ULift.moduleEquiv.{u, u, v}.symm).toLinearMap.comp <| mkQ _)
          ((DirectSum.toModule _ _ _ fun i => (liftQSpanSingleton (p ^ k i)
              (LinearMap.toSpanSingleton _ _ _) (this i).choose_spec.left : R ‚ß∏ _ ‚Üí‚Çó[R] _)).comp
            ULift.moduleEquiv.toLinearMap) (R ‚àô s j).injective_subtype ?_ ?_).symm.trans
          (((quotTorsionOfEquivSpanSingleton R N (s j)).symm.trans
          (quotEquivOfEq (torsionOf R N (s j)) _
          (Ideal.torsionOf_eq_span_pow_pOrder hp hN (s j)))).prod
          (ULift.moduleEquiv))).trans
          (@DirectSum.lequivProdDirectSum R _ _ _
          (fun i => R ‚ß∏ R ‚àô p ^ @Option.rec _ (fun _ => ‚Ñï) (pOrder hN <| s j) k i) _ _).symm).trans
          (DirectSum.lequivCongrLeft R (finSuccEquiv d).symm)
        ¬∑ rw [range_subtype, LinearEquiv.ker_comp, ker_mkQ]
          -- üéâ no goals
        ¬∑ rw [‚Üê f.comp_coe, LinearMap.comp_assoc, LinearMap.comp_assoc,
            LinearEquiv.toLinearMap_symm_comp_eq, LinearMap.comp_id, ‚Üê LinearMap.comp_assoc,
            ‚Üê LinearMap.comp_assoc]
          suffices (f.toLinearMap.comp (R ‚àô s j).mkQ).comp _ = LinearMap.id by
            rw [this, LinearMap.id_comp]
          ext i : 3
          -- ‚ä¢ ‚Üë(LinearMap.comp (LinearMap.comp (LinearMap.comp (LinearMap.comp (‚Üëf) (mkQ ( ‚Ä¶
          simp only [LinearMap.coe_comp, Function.comp_apply, mkQ_apply]
          -- ‚ä¢ ‚Üë‚Üëf (Submodule.Quotient.mk (‚Üë(DirectSum.toModule R (Fin d) N fun i => liftQS ‚Ä¶
          rw [LinearEquiv.coe_toLinearMap, LinearMap.id_apply, DirectSum.toModule_lof,
            liftQSpanSingleton_apply, LinearMap.toSpanSingleton_one, Ideal.Quotient.mk_eq_mk,
            map_one, (this i).choose_spec.right]
    ¬∑ exact (mk_surjective _).forall.mpr fun x =>
        ‚ü®(@hN x).choose, by rw [‚Üê Quotient.mk_smul, (@hN x).choose_spec, Quotient.mk_zero]‚ü©
    ¬∑ have hs' := congr_arg (Submodule.map <| mkQ <| R ‚àô s j) hs
      -- ‚ä¢ Submodule.span R (Set.range s') = ‚ä§
      rw [Submodule.map_span, Submodule.map_top, range_mkQ] at hs'; simp only [mkQ_apply] at hs'
      -- ‚ä¢ Submodule.span R (Set.range s') = ‚ä§
                                                                    -- ‚ä¢ Submodule.span R (Set.range s') = ‚ä§
      simp only; rw [‚Üê Function.comp.assoc, Set.range_comp (_ ‚àò s), Fin.range_succAbove]
      -- ‚ä¢ Submodule.span R (Set.range (Submodule.Quotient.mk ‚àò s ‚àò Fin.succAbove j)) = ‚ä§
                 -- ‚ä¢ Submodule.span R (Submodule.Quotient.mk ‚àò s '' {j}·∂ú) = ‚ä§
      rw [‚Üê Set.range_comp, ‚Üê Set.insert_image_compl_eq_range _ j, Function.comp_apply,
        (Quotient.mk_eq_zero _).mpr (Submodule.mem_span_singleton_self _), span_insert_zero] at hs'
      exact hs'
      -- üéâ no goals
#align module.torsion_by_prime_power_decomposition Module.torsion_by_prime_power_decomposition

end PTorsion

/-- A finitely generated torsion module over a PID is isomorphic to a direct sum of some
  `R ‚ß∏ R ‚àô (p i ^ e i)` where the `p i ^ e i` are prime powers.-/
theorem equiv_directSum_of_isTorsion [h' : Module.Finite R N] (hN : Module.IsTorsion R N) :
    ‚àÉ (Œπ : Type u) (_ : Fintype Œπ) (p : Œπ ‚Üí R) (_ : ‚àÄ i, Irreducible <| p i) (e : Œπ ‚Üí ‚Ñï),
      Nonempty <| N ‚âÉ‚Çó[R] ‚®Å i : Œπ, R ‚ß∏ R ‚àô p i ^ e i := by
  obtain ‚ü®I, fI, _, p, hp, e, h‚ü© := Submodule.exists_isInternal_prime_power_torsion_of_pid hN
  -- ‚ä¢ ‚àÉ Œπ x p x e, Nonempty (N ‚âÉ‚Çó[R] ‚®Å (i : Œπ), R ‚ß∏ span R {p i ^ e i})
  haveI := fI
  -- ‚ä¢ ‚àÉ Œπ x p x e, Nonempty (N ‚âÉ‚Çó[R] ‚®Å (i : Œπ), R ‚ß∏ span R {p i ^ e i})
  have :
    ‚àÄ i,
      ‚àÉ (d : ‚Ñï) (k : Fin d ‚Üí ‚Ñï),
        Nonempty <| torsionBy R N (p i ^ e i) ‚âÉ‚Çó[R] ‚®Å j, R ‚ß∏ R ‚àô p i ^ k j := by
    have := isNoetherian_of_isNoetherianRing_of_finite R N
    haveI := fun i => isNoetherian_submodule' (torsionBy R N <| p i ^ e i)
    exact fun i =>
      torsion_by_prime_power_decomposition.{u, v} (hp i)
        ((isTorsion'_powers_iff <| p i).mpr fun x => ‚ü®e i, smul_torsionBy _ _‚ü©)
  classical
  refine'
    ‚ü®Œ£ i, Fin (this i).choose, inferInstance, fun ‚ü®i, _‚ü© => p i, fun ‚ü®i, _‚ü© => hp i, fun ‚ü®i, j‚ü© =>
      (this i).choose_spec.choose j,
      ‚ü®(LinearEquiv.ofBijective (DirectSum.coeLinearMap _) h).symm.trans <|
          (DFinsupp.mapRange.linearEquiv fun i => (this i).choose_spec.choose_spec.some).trans <|
            (DirectSum.sigmaLcurryEquiv R).symm.trans
              (DFinsupp.mapRange.linearEquiv fun i => quotEquivOfEq _ _ _)‚ü©‚ü©
  cases' i with i j
  simp only
#align module.equiv_direct_sum_of_is_torsion Module.equiv_directSum_of_isTorsion

/-- **Structure theorem of finitely generated modules over a PID** : A finitely generated
  module over a PID is isomorphic to the product of a free module and a direct sum of some
  `R ‚ß∏ R ‚àô (p i ^ e i)` where the `p i ^ e i` are prime powers.-/
theorem equiv_free_prod_directSum [h' : Module.Finite R N] :
    ‚àÉ (n : ‚Ñï) (Œπ : Type u) (_ : Fintype Œπ) (p : Œπ ‚Üí R) (_ : ‚àÄ i, Irreducible <| p i) (e : Œπ ‚Üí ‚Ñï),
      Nonempty <| N ‚âÉ‚Çó[R] (Fin n ‚Üí‚ÇÄ R) √ó ‚®Å i : Œπ, R ‚ß∏ R ‚àô p i ^ e i := by
  have := isNoetherian_of_isNoetherianRing_of_finite R N
  -- ‚ä¢ ‚àÉ n Œπ x p x e, Nonempty (N ‚âÉ‚Çó[R] (Fin n ‚Üí‚ÇÄ R) √ó ‚®Å (i : Œπ), R ‚ß∏ span R {p i ^ ‚Ä¶
  haveI := isNoetherian_submodule' (torsion R N)
  -- ‚ä¢ ‚àÉ n Œπ x p x e, Nonempty (N ‚âÉ‚Çó[R] (Fin n ‚Üí‚ÇÄ R) √ó ‚®Å (i : Œπ), R ‚ß∏ span R {p i ^ ‚Ä¶
  haveI := Module.Finite.of_surjective _ (torsion R N).mkQ_surjective
  -- ‚ä¢ ‚àÉ n Œπ x p x e, Nonempty (N ‚âÉ‚Çó[R] (Fin n ‚Üí‚ÇÄ R) √ó ‚®Å (i : Œπ), R ‚ß∏ span R {p i ^ ‚Ä¶
  obtain ‚ü®I, fI, p, hp, e, ‚ü®h‚ü©‚ü© :=
    equiv_directSum_of_isTorsion.{u, v} (@torsion_isTorsion R N _ _ _)
  obtain ‚ü®n, ‚ü®g‚ü©‚ü© := @Module.basisOfFiniteTypeTorsionFree' R _ _ _ (N ‚ß∏ torsion R N) _ _ _ _
  -- ‚ä¢ ‚àÉ n Œπ x p x e, Nonempty (N ‚âÉ‚Çó[R] (Fin n ‚Üí‚ÇÄ R) √ó ‚®Å (i : Œπ), R ‚ß∏ span R {p i ^ ‚Ä¶
  haveI : Module.Projective R (N ‚ß∏ torsion R N) := Module.Projective.of_basis ‚ü®g‚ü©
  -- ‚ä¢ ‚àÉ n Œπ x p x e, Nonempty (N ‚âÉ‚Çó[R] (Fin n ‚Üí‚ÇÄ R) √ó ‚®Å (i : Œπ), R ‚ß∏ span R {p i ^ ‚Ä¶
  obtain ‚ü®f, hf‚ü© := Module.projective_lifting_property _ LinearMap.id (torsion R N).mkQ_surjective
  -- ‚ä¢ ‚àÉ n Œπ x p x e, Nonempty (N ‚âÉ‚Çó[R] (Fin n ‚Üí‚ÇÄ R) √ó ‚®Å (i : Œπ), R ‚ß∏ span R {p i ^ ‚Ä¶
  refine'
    ‚ü®n, I, fI, p, hp, e,
      ‚ü®(lequivProdOfRightSplitExact (torsion R N).injective_subtype _ hf).symm.trans <|
          (h.prod g).trans <| LinearEquiv.prodComm.{u, u} R _ (Fin n ‚Üí‚ÇÄ R) ‚ü©‚ü©
  rw [range_subtype, ker_mkQ]
  -- üéâ no goals
#align module.equiv_free_prod_direct_sum Module.equiv_free_prod_directSum

end Module
