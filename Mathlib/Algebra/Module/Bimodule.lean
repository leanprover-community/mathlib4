/-
Copyright (c) 2022 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.RingTheory.TensorProduct

#align_import algebra.module.bimodule from "leanprover-community/mathlib"@"58cef51f7a819e7227224461e392dee423302f2d"

/-!
# Bimodules

One frequently encounters situations in which several sets of scalars act on a single space, subject
to compatibility condition(s). A distinguished instance of this is the theory of bimodules: one has
two rings `R`, `S` acting on an additive group `M`, with `R` acting covariantly ("on the left")
and `S` acting contravariantly ("on the right"). The compatibility condition is just:
`(r â€¢ m) â€¢ s = r â€¢ (m â€¢ s)` for all `r : R`, `s : S`, `m : M`.

This situation can be set up in Mathlib as:
```lean
variables (R S M : Type*) [Ring R] [Ring S]
variables [AddCommGroup M] [Module R M] [Module Sáµáµ’áµ– M] [SMulCommClass R Sáµáµ’áµ– M]
```
The key fact is:
```lean
example : Module (R âŠ—[â„•] Sáµáµ’áµ–) M := TensorProduct.Algebra.module
```
Note that the corresponding result holds for the canonically isomorphic ring `R âŠ—[â„¤] Sáµáµ’áµ–` but it is
preferable to use the `R âŠ—[â„•] Sáµáµ’áµ–` instance since it works without additive inverses.

Bimodules are thus just a special case of `Module`s and most of their properties follow from the
theory of `Module`s. In particular a two-sided Submodule of a bimodule is simply a term of type
`Submodule (R âŠ—[â„•] Sáµáµ’áµ–) M`.

This file is a place to collect results which are specific to bimodules.

## Main definitions

 * `Subbimodule.mk`
 * `Subbimodule.smul_mem`
 * `Subbimodule.smul_mem'`
 * `Subbimodule.toSubmodule`
 * `Subbimodule.toSubmodule'`

## Implementation details

For many definitions and lemmas it is preferable to set things up without opposites, i.e., as:
`[Module S M] [SMulCommClass R S M]` rather than `[Module Sáµáµ’áµ– M] [SMulCommClass R Sáµáµ’áµ– M]`.
The corresponding results for opposites then follow automatically and do not require taking
advantage of the fact that `(Sáµáµ’áµ–)áµáµ’áµ–` is defeq to `S`.

## TODO

Develop the theory of two-sided ideals, which have type `Submodule (R âŠ—[â„•] Ráµáµ’áµ–) R`.

-/


open TensorProduct

attribute [local instance] TensorProduct.Algebra.module

namespace Subbimodule

section Algebra

variable {R A B M : Type*}

variable [CommSemiring R] [AddCommMonoid M] [Module R M]

variable [Semiring A] [Semiring B] [Module A M] [Module B M]

variable [Algebra R A] [Algebra R B]

variable [IsScalarTower R A M] [IsScalarTower R B M]

variable [SMulCommClass A B M]

/-- A constructor for a subbimodule which demands closure under the two sets of scalars
individually, rather than jointly via their tensor product.

Note that `R` plays no role but it is convenient to make this generalisation to support the cases
`R = â„•` and `R = â„¤` which both show up naturally. See also `Subbimodule.baseChange`. -/
@[simps]
def mk (p : AddSubmonoid M) (hA : âˆ€ (a : A) {m : M}, m âˆˆ p â†’ a â€¢ m âˆˆ p)
    (hB : âˆ€ (b : B) {m : M}, m âˆˆ p â†’ b â€¢ m âˆˆ p) : Submodule (A âŠ—[R] B) M :=
  { p with
    carrier := p
    smul_mem' := fun ab m =>
      TensorProduct.induction_on ab (fun _ => by simpa only [zero_smul] using p.zero_mem)
                                                 -- ðŸŽ‰ no goals
        (fun a b hm => by simpa only [TensorProduct.Algebra.smul_def] using hA a (hB b hm))
                          -- ðŸŽ‰ no goals
        fun z w hz hw hm => by simpa only [add_smul] using p.add_mem (hz hm) (hw hm) }
                               -- ðŸŽ‰ no goals
#align subbimodule.mk Subbimodule.mk

theorem smul_mem (p : Submodule (A âŠ—[R] B) M) (a : A) {m : M} (hm : m âˆˆ p) : a â€¢ m âˆˆ p := by
  suffices a â€¢ m = a âŠ—â‚œ[R] (1 : B) â€¢ m by exact this.symm â–¸ p.smul_mem _ hm
  -- âŠ¢ a â€¢ m = a âŠ—â‚œ[R] 1 â€¢ m
  simp [TensorProduct.Algebra.smul_def]
  -- ðŸŽ‰ no goals
#align subbimodule.smul_mem Subbimodule.smul_mem

theorem smul_mem' (p : Submodule (A âŠ—[R] B) M) (b : B) {m : M} (hm : m âˆˆ p) : b â€¢ m âˆˆ p := by
  suffices b â€¢ m = (1 : A) âŠ—â‚œ[R] b â€¢ m by exact this.symm â–¸ p.smul_mem _ hm
  -- âŠ¢ b â€¢ m = 1 âŠ—â‚œ[R] b â€¢ m
  simp [TensorProduct.Algebra.smul_def]
  -- ðŸŽ‰ no goals
#align subbimodule.smul_mem' Subbimodule.smul_mem'

/-- If `A` and `B` are also `Algebra`s over yet another set of scalars `S` then we may "base change"
from `R` to `S`. -/
@[simps!]
def baseChange (S : Type*) [CommSemiring S] [Module S M] [Algebra S A] [Algebra S B]
    [IsScalarTower S A M] [IsScalarTower S B M] (p : Submodule (A âŠ—[R] B) M) :
    Submodule (A âŠ—[S] B) M :=
  mk p.toAddSubmonoid (smul_mem p) (smul_mem' p)
#align subbimodule.base_change Subbimodule.baseChange

/-- Forgetting the `B` action, a `Submodule` over `A âŠ—[R] B` is just a `Submodule` over `A`. -/
@[simps]
def toSubmodule (p : Submodule (A âŠ—[R] B) M) : Submodule A M :=
  { p with
    carrier := p
    smul_mem' := smul_mem p }
#align subbimodule.to_submodule Subbimodule.toSubmodule

/-- Forgetting the `A` action, a `Submodule` over `A âŠ—[R] B` is just a `Submodule` over `B`. -/
@[simps]
def toSubmodule' (p : Submodule (A âŠ—[R] B) M) : Submodule B M :=
  { p with
    carrier := p
    smul_mem' := smul_mem' p }
#align subbimodule.to_submodule' Subbimodule.toSubmodule'

end Algebra

section Ring

variable (R S M : Type*) [Ring R] [Ring S]

variable [AddCommGroup M] [Module R M] [Module S M] [SMulCommClass R S M]

/-- A `Submodule` over `R âŠ—[â„•] S` is naturally also a `Submodule` over the canonically-isomorphic
ring `R âŠ—[â„¤] S`. -/
@[simps!]
def toSubbimoduleInt (p : Submodule (R âŠ—[â„•] S) M) : Submodule (R âŠ—[â„¤] S) M :=
  baseChange â„¤ p
#align subbimodule.to_subbimodule_int Subbimodule.toSubbimoduleInt

/-- A `Submodule` over `R âŠ—[â„¤] S` is naturally also a `Submodule` over the canonically-isomorphic
ring `R âŠ—[â„•] S`. -/
@[simps!]
def toSubbimoduleNat (p : Submodule (R âŠ—[â„¤] S) M) : Submodule (R âŠ—[â„•] S) M :=
  baseChange â„• p
#align subbimodule.to_subbimodule_nat Subbimodule.toSubbimoduleNat

end Ring

end Subbimodule
