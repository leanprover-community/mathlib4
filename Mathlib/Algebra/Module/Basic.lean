/-
Copyright (c) 2015 Nathaniel Thomas. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nathaniel Thomas, Jeremy Avigad, Johannes H√∂lzl, Mario Carneiro
-/
import Mathlib.Algebra.SMulWithZero
import Mathlib.Algebra.Field.Defs
import Mathlib.Data.Rat.Defs
import Mathlib.Data.Rat.Basic
import Mathlib.GroupTheory.GroupAction.Group
import Mathlib.Tactic.Abel

#align_import algebra.module.basic from "leanprover-community/mathlib"@"30413fc89f202a090a54d78e540963ed3de0056e"

/-!
# Modules over a ring

In this file we define

* `Module R M` : an additive commutative monoid `M` is a `Module` over a
  `Semiring R` if for `r : R` and `x : M` their "scalar multiplication" `r ‚Ä¢ x : M` is defined, and
  the operation `‚Ä¢` satisfies some natural associativity and distributivity axioms similar to those
  on a ring.

## Implementation notes

In typical mathematical usage, our definition of `Module` corresponds to "semimodule", and the
word "module" is reserved for `Module R M` where `R` is a `Ring` and `M` an `AddCommGroup`.
If `R` is a `Field` and `M` an `AddCommGroup`, `M` would be called an `R`-vector space.
Since those assumptions can be made by changing the typeclasses applied to `R` and `M`,
without changing the axioms in `Module`, mathlib calls everything a `Module`.

In older versions of mathlib3, we had separate `semimodule` and `vector_space` abbreviations.
This caused inference issues in some cases, while not providing any real advantages, so we decided
to use a canonical `Module` typeclass throughout.

## Tags

semimodule, module, vector space
-/


open Function

universe u v

variable {Œ± R k S M M‚ÇÇ M‚ÇÉ Œπ : Type*}

/-- A module is a generalization of vector spaces to a scalar semiring.
  It consists of a scalar semiring `R` and an additive monoid of "vectors" `M`,
  connected by a "scalar multiplication" operation `r ‚Ä¢ x : M`
  (where `r : R` and `x : M`) with some natural associativity and
  distributivity axioms similar to those on a ring. -/
@[ext]
class Module (R : Type u) (M : Type v) [Semiring R] [AddCommMonoid M] extends
  DistribMulAction R M where
  /-- Scalar multiplication distributes over addition from the right. -/
  protected add_smul : ‚àÄ (r s : R) (x : M), (r + s) ‚Ä¢ x = r ‚Ä¢ x + s ‚Ä¢ x
  /-- Scalar multiplication by zero gives zero. -/
  protected zero_smul : ‚àÄ x : M, (0 : R) ‚Ä¢ x = 0
#align module Module
#align module.ext Module.ext
#align module.ext_iff Module.ext_iff

section AddCommMonoid

variable [Semiring R] [AddCommMonoid M] [Module R M] (r s : R) (x y : M)

-- see Note [lower instance priority]
/-- A module over a semiring automatically inherits a `MulActionWithZero` structure. -/
instance (priority := 100) Module.toMulActionWithZero : MulActionWithZero R M :=
  { (inferInstance : MulAction R M) with
    smul_zero := smul_zero
    zero_smul := Module.zero_smul }
#align module.to_mul_action_with_zero Module.toMulActionWithZero

instance AddCommMonoid.natModule : Module ‚Ñï M where
  one_smul := one_nsmul
  mul_smul m n a := mul_nsmul' a m n
  smul_add n a b := nsmul_add a b n
  smul_zero := nsmul_zero
  zero_smul := zero_nsmul
  add_smul r s x := add_nsmul x r s
#align add_comm_monoid.nat_module AddCommMonoid.natModule

theorem AddMonoid.End.nat_cast_def (n : ‚Ñï) :
    (‚Üën : AddMonoid.End M) = DistribMulAction.toAddMonoidEnd ‚Ñï M n :=
  rfl
#align add_monoid.End.nat_cast_def AddMonoid.End.nat_cast_def

theorem add_smul : (r + s) ‚Ä¢ x = r ‚Ä¢ x + s ‚Ä¢ x :=
  Module.add_smul r s x
#align add_smul add_smul

theorem Convex.combo_self {a b : R} (h : a + b = 1) (x : M) : a ‚Ä¢ x + b ‚Ä¢ x = x := by
  rw [‚Üê add_smul, h, one_smul]
  -- üéâ no goals
#align convex.combo_self Convex.combo_self

variable (R)

-- Porting note: this is the letter of the mathlib3 version, but not really the spirit
theorem two_smul : (2 : R) ‚Ä¢ x = x + x := by rw [‚Üê one_add_one_eq_two, add_smul, one_smul]
                                             -- üéâ no goals
#align two_smul two_smul

set_option linter.deprecated false in
@[deprecated] theorem two_smul' : (2 : R) ‚Ä¢ x = bit0 x :=
  two_smul R x
#align two_smul' two_smul'

@[simp]
theorem invOf_two_smul_add_invOf_two_smul [Invertible (2 : R)] (x : M) :
    (‚Öü 2 : R) ‚Ä¢ x + (‚Öü 2 : R) ‚Ä¢ x = x :=
  Convex.combo_self invOf_two_add_invOf_two _
#align inv_of_two_smul_add_inv_of_two_smul invOf_two_smul_add_invOf_two_smul

/-- Pullback a `Module` structure along an injective additive monoid homomorphism.
See note [reducible non-instances]. -/
@[reducible]
protected def Function.Injective.module [AddCommMonoid M‚ÇÇ] [SMul R M‚ÇÇ] (f : M‚ÇÇ ‚Üí+ M)
    (hf : Injective f) (smul : ‚àÄ (c : R) (x), f (c ‚Ä¢ x) = c ‚Ä¢ f x) : Module R M‚ÇÇ :=
  { hf.distribMulAction f smul with
    add_smul := fun c‚ÇÅ c‚ÇÇ x => hf <| by simp only [smul, f.map_add, add_smul]
                                        -- üéâ no goals
    zero_smul := fun x => hf <| by simp only [smul, zero_smul, f.map_zero] }
                                   -- üéâ no goals
#align function.injective.module Function.Injective.module

/-- Pushforward a `Module` structure along a surjective additive monoid homomorphism. -/
protected def Function.Surjective.module [AddCommMonoid M‚ÇÇ] [SMul R M‚ÇÇ] (f : M ‚Üí+ M‚ÇÇ)
    (hf : Surjective f) (smul : ‚àÄ (c : R) (x), f (c ‚Ä¢ x) = c ‚Ä¢ f x) : Module R M‚ÇÇ :=
  { toDistribMulAction := hf.distribMulAction f smul
    add_smul := fun c‚ÇÅ c‚ÇÇ x => by
      rcases hf x with ‚ü®x, rfl‚ü©
      -- ‚ä¢ (c‚ÇÅ + c‚ÇÇ) ‚Ä¢ ‚Üëf x = c‚ÇÅ ‚Ä¢ ‚Üëf x + c‚ÇÇ ‚Ä¢ ‚Üëf x
      simp only [add_smul, ‚Üê smul, ‚Üê f.map_add]
      -- üéâ no goals
    zero_smul := fun x => by
      rcases hf x with ‚ü®x, rfl‚ü©
      -- ‚ä¢ 0 ‚Ä¢ ‚Üëf x = 0
      rw [‚Üê f.map_zero, ‚Üê smul, zero_smul] }
      -- üéâ no goals
#align function.surjective.module Function.Surjective.module

/-- Push forward the action of `R` on `M` along a compatible surjective map `f : R ‚Üí+* S`.

See also `Function.Surjective.mulActionLeft` and `Function.Surjective.distribMulActionLeft`.
-/
@[reducible]
def Function.Surjective.moduleLeft {R S M : Type*} [Semiring R] [AddCommMonoid M] [Module R M]
    [Semiring S] [SMul S M] (f : R ‚Üí+* S) (hf : Function.Surjective f)
    (hsmul : ‚àÄ (c) (x : M), f c ‚Ä¢ x = c ‚Ä¢ x) : Module S M :=
  { hf.distribMulActionLeft f.toMonoidHom hsmul with
    zero_smul := fun x => by rw [‚Üê f.map_zero, hsmul, zero_smul]
                             -- üéâ no goals
                                               -- üéâ no goals
    add_smul := hf.forall‚ÇÇ.mpr fun a b x => by simp only [‚Üê f.map_add, hsmul, add_smul] }
#align function.surjective.module_left Function.Surjective.moduleLeft

variable {R} (M)

/-- Compose a `Module` with a `RingHom`, with action `f s ‚Ä¢ m`.

See note [reducible non-instances]. -/
@[reducible]
def Module.compHom [Semiring S] (f : S ‚Üí+* R) : Module S M :=
  { MulActionWithZero.compHom M f.toMonoidWithZeroHom, DistribMulAction.compHom M (f : S ‚Üí* R) with
    smul := SMul.comp.smul f
    -- Porting note: the `show f (r + s) ‚Ä¢ x = f r ‚Ä¢ x + f s ‚Ä¢ x ` wasn't needed in mathlib3.
    -- Somehow, now that `SMul` is heterogeneous, it can't unfold earlier fields of a definition for
    -- use in later fields.  See
    -- https://leanprover.zulipchat.com/#narrow/stream/287929-mathlib4/topic/Heterogeneous.20scalar.20multiplication
    add_smul := fun r s x => show f (r + s) ‚Ä¢ x = f r ‚Ä¢ x + f s ‚Ä¢ x by simp [add_smul] }
                                                                       -- üéâ no goals
#align module.comp_hom Module.compHom

variable (R)

/-- `(‚Ä¢)` as an `AddMonoidHom`.

This is a stronger version of `DistribMulAction.toAddMonoidEnd` -/
@[simps! apply_apply]
def Module.toAddMonoidEnd : R ‚Üí+* AddMonoid.End M :=
  { DistribMulAction.toAddMonoidEnd R M with
    -- Porting note: the two `show`s weren't needed in mathlib3.
    -- Somehow, now that `SMul` is heterogeneous, it can't unfold earlier fields of a definition for
    -- use in later fields.  See
    -- https://leanprover.zulipchat.com/#narrow/stream/287929-mathlib4/topic/Heterogeneous.20scalar.20multiplication
    map_zero' := AddMonoidHom.ext fun r => show (0:R) ‚Ä¢ r = 0 by simp
                                                                 -- üéâ no goals
    map_add' := fun x y =>
      AddMonoidHom.ext fun r => show (x + y) ‚Ä¢ r = x ‚Ä¢ r + y ‚Ä¢ r by simp [add_smul] }
                                                                    -- üéâ no goals
#align module.to_add_monoid_End Module.toAddMonoidEnd
#align module.to_add_monoid_End_apply_apply Module.toAddMonoidEnd_apply_apply

/-- A convenience alias for `Module.toAddMonoidEnd` as an `AddMonoidHom`, usually to allow the
use of `AddMonoidHom.flip`. -/
def smulAddHom : R ‚Üí+ M ‚Üí+ M :=
  (Module.toAddMonoidEnd R M).toAddMonoidHom
#align smul_add_hom smulAddHom

variable {R M}

@[simp]
theorem smulAddHom_apply (r : R) (x : M) : smulAddHom R M r x = r ‚Ä¢ x :=
  rfl
#align smul_add_hom_apply smulAddHom_apply

theorem Module.eq_zero_of_zero_eq_one (zero_eq_one : (0 : R) = 1) : x = 0 := by
  rw [‚Üê one_smul R x, ‚Üê zero_eq_one, zero_smul]
  -- üéâ no goals
#align module.eq_zero_of_zero_eq_one Module.eq_zero_of_zero_eq_one

@[simp]
theorem smul_add_one_sub_smul {R : Type*} [Ring R] [Module R M] {r : R} {m : M} :
    r ‚Ä¢ m + (1 - r) ‚Ä¢ m = m := by rw [‚Üê add_smul, add_sub_cancel'_right, one_smul]
                                  -- üéâ no goals
#align smul_add_one_sub_smul smul_add_one_sub_smul

end AddCommMonoid


section AddCommGroup

variable (R M) [Semiring R] [AddCommGroup M]

instance AddCommGroup.intModule : Module ‚Ñ§ M where
  one_smul := one_zsmul
  mul_smul m n a := mul_zsmul a m n
  smul_add n a b := zsmul_add a b n
  smul_zero := zsmul_zero
  zero_smul := zero_zsmul
  add_smul r s x := add_zsmul x r s
#align add_comm_group.int_module AddCommGroup.intModule

theorem AddMonoid.End.int_cast_def (z : ‚Ñ§) :
    (‚Üëz : AddMonoid.End M) = DistribMulAction.toAddMonoidEnd ‚Ñ§ M z :=
  rfl
#align add_monoid.End.int_cast_def AddMonoid.End.int_cast_def

/-- A structure containing most informations as in a module, except the fields `zero_smul`
and `smul_zero`. As these fields can be deduced from the other ones when `M` is an `AddCommGroup`,
this provides a way to construct a module structure by checking less properties, in
`Module.ofCore`. -/
-- Porting note: removed @[nolint has_nonempty_instance]
structure Module.Core extends SMul R M where
  /-- Scalar multiplication distributes over addition from the left. -/
  smul_add : ‚àÄ (r : R) (x y : M), r ‚Ä¢ (x + y) = r ‚Ä¢ x + r ‚Ä¢ y
  /-- Scalar multiplication distributes over addition from the right. -/
  add_smul : ‚àÄ (r s : R) (x : M), (r + s) ‚Ä¢ x = r ‚Ä¢ x + s ‚Ä¢ x
  /-- Scalar multiplication distributes over multiplication from the right. -/
  mul_smul : ‚àÄ (r s : R) (x : M), (r * s) ‚Ä¢ x = r ‚Ä¢ s ‚Ä¢ x
  /-- Scalar multiplication by one is the identity. -/
  one_smul : ‚àÄ x : M, (1 : R) ‚Ä¢ x = x
#align module.core Module.Core

variable {R M}

/-- Define `Module` without proving `zero_smul` and `smul_zero` by using an auxiliary
structure `Module.Core`, when the underlying space is an `AddCommGroup`. -/
def Module.ofCore (H : Module.Core R M) : Module R M :=
  letI := H.toSMul
  { H with
    zero_smul := fun x =>
      (AddMonoidHom.mk' (fun r : R => r ‚Ä¢ x) fun r s => H.add_smul r s x).map_zero
    smul_zero := fun r => (AddMonoidHom.mk' ((¬∑ ‚Ä¢ ¬∑) r) (H.smul_add r)).map_zero }
#align module.of_core Module.ofCore

theorem Convex.combo_eq_smul_sub_add [Module R M] {x y : M} {a b : R} (h : a + b = 1) :
    a ‚Ä¢ x + b ‚Ä¢ y = b ‚Ä¢ (y - x) + x :=
  calc
    a ‚Ä¢ x + b ‚Ä¢ y = b ‚Ä¢ y - b ‚Ä¢ x + (a ‚Ä¢ x + b ‚Ä¢ x) := by abel
                                                          -- üéâ no goals
                                                          -- üéâ no goals
    _ = b ‚Ä¢ (y - x) + x := by rw [smul_sub, Convex.combo_self h]
                              -- üéâ no goals
#align convex.combo_eq_smul_sub_add Convex.combo_eq_smul_sub_add

end AddCommGroup

-- We'll later use this to show `Module ‚Ñï M` and `Module ‚Ñ§ M` are subsingletons.
/-- A variant of `Module.ext` that's convenient for term-mode. -/
theorem Module.ext' {R : Type*} [Semiring R] {M : Type*} [AddCommMonoid M] (P Q : Module R M)
    (w : ‚àÄ (r : R) (m : M), (haveI := P; r ‚Ä¢ m) = (haveI := Q; r ‚Ä¢ m)) :
    P = Q := by
  ext
  -- ‚ä¢ SMul.smul x‚úù¬π x‚úù = SMul.smul x‚úù¬π x‚úù
  exact w _ _
  -- üéâ no goals
#align module.ext' Module.ext'

section Module

variable [Ring R] [AddCommGroup M] [Module R M] (r s : R) (x y : M)

@[simp]
theorem neg_smul : -r ‚Ä¢ x = -(r ‚Ä¢ x) :=
  eq_neg_of_add_eq_zero_left <| by rw [‚Üê add_smul, add_left_neg, zero_smul]
                                   -- üéâ no goals
#align neg_smul neg_smul

-- Porting note: simp can prove this
--@[simp]
theorem neg_smul_neg : -r ‚Ä¢ -x = r ‚Ä¢ x := by rw [neg_smul, smul_neg, neg_neg]
                                             -- üéâ no goals
#align neg_smul_neg neg_smul_neg

@[simp]
theorem Units.neg_smul (u : RÀ£) (x : M) : -u ‚Ä¢ x = -(u ‚Ä¢ x) := by
  rw [Units.smul_def, Units.val_neg, _root_.neg_smul, Units.smul_def]
  -- üéâ no goals
#align units.neg_smul Units.neg_smul

variable (R)

theorem neg_one_smul (x : M) : (-1 : R) ‚Ä¢ x = -x := by simp
                                                       -- üéâ no goals
#align neg_one_smul neg_one_smul

variable {R}

theorem sub_smul (r s : R) (y : M) : (r - s) ‚Ä¢ y = r ‚Ä¢ y - s ‚Ä¢ y := by
  simp [add_smul, sub_eq_add_neg]
  -- üéâ no goals
#align sub_smul sub_smul

end Module

variable (R)

/-- An `AddCommMonoid` that is a `Module` over a `Ring` carries a natural `AddCommGroup`
structure.
See note [reducible non-instances]. -/
@[reducible]
def Module.addCommMonoidToAddCommGroup [Ring R] [AddCommMonoid M] [Module R M] : AddCommGroup M :=
  { (inferInstance : AddCommMonoid M) with
    neg := fun a => (-1 : R) ‚Ä¢ a
    add_left_neg := fun a =>
      show (-1 : R) ‚Ä¢ a + a = 0 by
        nth_rw 2 [‚Üê one_smul R a]
        -- ‚ä¢ -1 ‚Ä¢ a + 1 ‚Ä¢ a = 0
        rw [‚Üê add_smul, add_left_neg, zero_smul]
        -- üéâ no goals
                               -- üéâ no goals
    zsmul := fun z a => (z : R) ‚Ä¢ a
                                 -- üéâ no goals
    zsmul_zero' := fun a => by simpa only [Int.cast_zero] using zero_smul R a
                                -- üéâ no goals
    zsmul_succ' := fun z a => by simp [add_comm, add_smul]
    zsmul_neg' := fun z a => by simp [‚Üêsmul_assoc, neg_one_smul] }
#align module.add_comm_monoid_to_add_comm_group Module.addCommMonoidToAddCommGroup

variable {R}

/-- A module over a `Subsingleton` semiring is a `Subsingleton`. We cannot register this
as an instance because Lean has no way to guess `R`. -/
protected theorem Module.subsingleton (R M : Type*) [Semiring R] [Subsingleton R] [AddCommMonoid M]
    [Module R M] : Subsingleton M :=
  MulActionWithZero.subsingleton R M
#align module.subsingleton Module.subsingleton

/-- A semiring is `Nontrivial` provided that there exists a nontrivial module over this semiring. -/
protected theorem Module.nontrivial (R M : Type*) [Semiring R] [Nontrivial M] [AddCommMonoid M]
    [Module R M] : Nontrivial R :=
  MulActionWithZero.nontrivial R M
#align module.nontrivial Module.nontrivial

-- see Note [lower instance priority]
instance (priority := 910) Semiring.toModule [Semiring R] : Module R R where
  smul_add := mul_add
  add_smul := add_mul
  zero_smul := zero_mul
  smul_zero := mul_zero
#align semiring.to_module Semiring.toModule

-- see Note [lower instance priority]
/-- Like `Semiring.toModule`, but multiplies on the right. -/
instance (priority := 910) Semiring.toOppositeModule [Semiring R] : Module R·µê·µí·µñ R :=
  { MonoidWithZero.toOppositeMulActionWithZero R with
    smul_add := fun _ _ _ => add_mul _ _ _
    add_smul := fun _ _ _ => mul_add _ _ _ }
#align semiring.to_opposite_module Semiring.toOppositeModule

/-- A ring homomorphism `f : R ‚Üí+* M` defines a module structure by `r ‚Ä¢ x = f r * x`. -/
def RingHom.toModule [Semiring R] [Semiring S] (f : R ‚Üí+* S) : Module R S :=
  Module.compHom S f
#align ring_hom.to_module RingHom.toModule

/-- The tautological action by `R ‚Üí+* R` on `R`.

This generalizes `Function.End.applyMulAction`. -/
instance RingHom.applyDistribMulAction [Semiring R] : DistribMulAction (R ‚Üí+* R) R where
  smul := (¬∑ <| ¬∑)
  smul_zero := RingHom.map_zero
  smul_add := RingHom.map_add
  one_smul _ := rfl
  mul_smul _ _ _ := rfl
#align ring_hom.apply_distrib_mul_action RingHom.applyDistribMulAction

@[simp]
protected theorem RingHom.smul_def [Semiring R] (f : R ‚Üí+* R) (a : R) : f ‚Ä¢ a = f a :=
  rfl
#align ring_hom.smul_def RingHom.smul_def

/-- `RingHom.applyDistribMulAction` is faithful. -/
instance RingHom.applyFaithfulSMul [Semiring R] : FaithfulSMul (R ‚Üí+* R) R :=
  ‚ü®fun {_ _} h => RingHom.ext h‚ü©
#align ring_hom.apply_has_faithful_smul RingHom.applyFaithfulSMul

section AddCommMonoid

variable [Semiring R] [AddCommMonoid M] [Module R M]

section

variable (R)

/-- `nsmul` is equal to any other module structure via a cast. -/
theorem nsmul_eq_smul_cast (n : ‚Ñï) (b : M) : n ‚Ä¢ b = (n : R) ‚Ä¢ b := by
  induction' n with n ih
  -- ‚ä¢ Nat.zero ‚Ä¢ b = ‚ÜëNat.zero ‚Ä¢ b
  ¬∑ rw [Nat.zero_eq, Nat.cast_zero, zero_smul, zero_smul]
    -- üéâ no goals
  ¬∑ rw [Nat.succ_eq_add_one, Nat.cast_succ, add_smul, add_smul, one_smul, ih, one_smul]
    -- üéâ no goals
#align nsmul_eq_smul_cast nsmul_eq_smul_cast

end

/-- Convert back any exotic `‚Ñï`-smul to the canonical instance. This should not be needed since in
mathlib all `AddCommMonoid`s should normally have exactly one `‚Ñï`-module structure by design.
-/
theorem nat_smul_eq_nsmul (h : Module ‚Ñï M) (n : ‚Ñï) (x : M) :
    @SMul.smul ‚Ñï M h.toSMul n x = n ‚Ä¢ x := by rw [nsmul_eq_smul_cast ‚Ñï n x, Nat.cast_id]; rfl
                                              -- ‚ä¢ SMul.smul n x = n ‚Ä¢ x
                                                                                          -- üéâ no goals
#align nat_smul_eq_nsmul nat_smul_eq_nsmul

/-- All `‚Ñï`-module structures are equal. Not an instance since in mathlib all `AddCommMonoid`
should normally have exactly one `‚Ñï`-module structure by design. -/
def AddCommMonoid.natModule.unique : Unique (Module ‚Ñï M) where
  default := by infer_instance
                -- üéâ no goals
  uniq P := (Module.ext' P _) fun n => by convert nat_smul_eq_nsmul P n
                                          -- üéâ no goals
#align add_comm_monoid.nat_module.unique AddCommMonoid.natModule.unique

instance AddCommMonoid.nat_isScalarTower : IsScalarTower ‚Ñï R M where
  smul_assoc n x y :=
    Nat.recOn n (by simp only [Nat.zero_eq, zero_smul])
                    -- üéâ no goals
    fun n ih => by simp only [Nat.succ_eq_add_one, add_smul, one_smul, ih]
                   -- üéâ no goals
#align add_comm_monoid.nat_is_scalar_tower AddCommMonoid.nat_isScalarTower

end AddCommMonoid

section AddCommGroup

variable [Semiring S] [Ring R] [AddCommGroup M] [Module S M] [Module R M]

section

variable (R)

/-- `zsmul` is equal to any other module structure via a cast. -/
theorem zsmul_eq_smul_cast (n : ‚Ñ§) (b : M) : n ‚Ä¢ b = (n : R) ‚Ä¢ b :=
  have : (smulAddHom ‚Ñ§ M).flip b = ((smulAddHom R M).flip b).comp (Int.castAddHom R) := by
    apply AddMonoidHom.ext_int
    -- ‚ä¢ ‚Üë(‚Üë(AddMonoidHom.flip (smulAddHom ‚Ñ§ M)) b) 1 = ‚Üë(AddMonoidHom.comp (‚Üë(AddMon ‚Ä¶
    simp
    -- üéâ no goals
  FunLike.congr_fun this n
#align zsmul_eq_smul_cast zsmul_eq_smul_cast

end

/-- Convert back any exotic `‚Ñ§`-smul to the canonical instance. This should not be needed since in
mathlib all `AddCommGroup`s should normally have exactly one `‚Ñ§`-module structure by design. -/
theorem int_smul_eq_zsmul (h : Module ‚Ñ§ M) (n : ‚Ñ§) (x : M) :
    @SMul.smul ‚Ñ§ M h.toSMul n x = n ‚Ä¢ x := by rw [zsmul_eq_smul_cast ‚Ñ§ n x, Int.cast_id]; rfl
                                              -- ‚ä¢ SMul.smul n x = n ‚Ä¢ x
                                                                                          -- üéâ no goals
#align int_smul_eq_zsmul int_smul_eq_zsmul

/-- All `‚Ñ§`-module structures are equal. Not an instance since in mathlib all `AddCommGroup`
should normally have exactly one `‚Ñ§`-module structure by design. -/
def AddCommGroup.intModule.unique : Unique (Module ‚Ñ§ M) where
  default := by infer_instance
                -- üéâ no goals
  uniq P := (Module.ext' P _) fun n => by convert int_smul_eq_zsmul P n
                                          -- üéâ no goals
#align add_comm_group.int_module.unique AddCommGroup.intModule.unique

end AddCommGroup

theorem map_int_cast_smul [AddCommGroup M] [AddCommGroup M‚ÇÇ] {F : Type*} [AddMonoidHomClass F M M‚ÇÇ]
    (f : F) (R S : Type*) [Ring R] [Ring S] [Module R M] [Module S M‚ÇÇ] (x : ‚Ñ§) (a : M) :
    f ((x : R) ‚Ä¢ a) = (x : S) ‚Ä¢ f a := by simp only [‚Üê zsmul_eq_smul_cast, map_zsmul]
                                          -- üéâ no goals
#align map_int_cast_smul map_int_cast_smul

theorem map_nat_cast_smul [AddCommMonoid M] [AddCommMonoid M‚ÇÇ] {F : Type*}
    [AddMonoidHomClass F M M‚ÇÇ] (f : F) (R S : Type*) [Semiring R] [Semiring S] [Module R M]
    [Module S M‚ÇÇ] (x : ‚Ñï) (a : M) : f ((x : R) ‚Ä¢ a) = (x : S) ‚Ä¢ f a := by
  simp only [‚Üê nsmul_eq_smul_cast, AddMonoidHom.map_nsmul, map_nsmul]
  -- üéâ no goals
#align map_nat_cast_smul map_nat_cast_smul

theorem map_inv_nat_cast_smul [AddCommMonoid M] [AddCommMonoid M‚ÇÇ] {F : Type*}
    [AddMonoidHomClass F M M‚ÇÇ] (f : F) (R S : Type*)
    [DivisionSemiring R] [DivisionSemiring S] [Module R M]
    [Module S M‚ÇÇ] (n : ‚Ñï) (x : M) : f ((n‚Åª¬π : R) ‚Ä¢ x) = (n‚Åª¬π : S) ‚Ä¢ f x := by
  by_cases hR : (n : R) = 0 <;> by_cases hS : (n : S) = 0
  -- ‚ä¢ ‚Üëf ((‚Üën)‚Åª¬π ‚Ä¢ x) = (‚Üën)‚Åª¬π ‚Ä¢ ‚Üëf x
                                -- ‚ä¢ ‚Üëf ((‚Üën)‚Åª¬π ‚Ä¢ x) = (‚Üën)‚Åª¬π ‚Ä¢ ‚Üëf x
                                -- ‚ä¢ ‚Üëf ((‚Üën)‚Åª¬π ‚Ä¢ x) = (‚Üën)‚Åª¬π ‚Ä¢ ‚Üëf x
  ¬∑ simp [hR, hS, map_zero f]
    -- üéâ no goals
  ¬∑ suffices ‚àÄ y, f y = 0 by rw [this, this, smul_zero]
    -- ‚ä¢ ‚àÄ (y : M), ‚Üëf y = 0
    clear x
    -- ‚ä¢ ‚àÄ (y : M), ‚Üëf y = 0
    intro x
    -- ‚ä¢ ‚Üëf x = 0
    rw [‚Üê inv_smul_smul‚ÇÄ hS (f x), ‚Üê map_nat_cast_smul f R S]
    -- ‚ä¢ (‚Üën)‚Åª¬π ‚Ä¢ ‚Üëf (‚Üën ‚Ä¢ x) = 0
    simp [hR, map_zero f]
    -- üéâ no goals
  ¬∑ suffices ‚àÄ y, f y = 0 by simp [this]
    -- ‚ä¢ ‚àÄ (y : M), ‚Üëf y = 0
    clear x
    -- ‚ä¢ ‚àÄ (y : M), ‚Üëf y = 0
    intro x
    -- ‚ä¢ ‚Üëf x = 0
    rw [‚Üê smul_inv_smul‚ÇÄ hR x, map_nat_cast_smul f R S, hS, zero_smul]
    -- üéâ no goals
  ¬∑ rw [‚Üê inv_smul_smul‚ÇÄ hS (f _), ‚Üê map_nat_cast_smul f R S, smul_inv_smul‚ÇÄ hR]
    -- üéâ no goals
#align map_inv_nat_cast_smul map_inv_nat_cast_smul

theorem map_inv_int_cast_smul [AddCommGroup M] [AddCommGroup M‚ÇÇ] {F : Type*}
    [AddMonoidHomClass F M M‚ÇÇ] (f : F) (R S : Type*) [DivisionRing R] [DivisionRing S] [Module R M]
    [Module S M‚ÇÇ] (z : ‚Ñ§) (x : M) : f ((z‚Åª¬π : R) ‚Ä¢ x) = (z‚Åª¬π : S) ‚Ä¢ f x := by
  obtain ‚ü®n, rfl | rfl‚ü© := z.eq_nat_or_neg
  -- ‚ä¢ ‚Üëf ((‚Üë‚Üën)‚Åª¬π ‚Ä¢ x) = (‚Üë‚Üën)‚Åª¬π ‚Ä¢ ‚Üëf x
  ¬∑ rw [Int.cast_Nat_cast, Int.cast_Nat_cast, map_inv_nat_cast_smul _ R S]
    -- üéâ no goals
  ¬∑ simp_rw [Int.cast_neg, Int.cast_Nat_cast, inv_neg, neg_smul, map_neg,
      map_inv_nat_cast_smul _ R S]
#align map_inv_int_cast_smul map_inv_int_cast_smul

theorem map_rat_cast_smul [AddCommGroup M] [AddCommGroup M‚ÇÇ] {F : Type*} [AddMonoidHomClass F M M‚ÇÇ]
    (f : F) (R S : Type*) [DivisionRing R] [DivisionRing S] [Module R M] [Module S M‚ÇÇ] (c : ‚Ñö)
    (x : M) : f ((c : R) ‚Ä¢ x) = (c : S) ‚Ä¢ f x := by
  rw [Rat.cast_def, Rat.cast_def, div_eq_mul_inv, div_eq_mul_inv, mul_smul, mul_smul,
    map_int_cast_smul f R S, map_inv_nat_cast_smul f R S]
#align map_rat_cast_smul map_rat_cast_smul

theorem map_rat_smul [AddCommGroup M] [AddCommGroup M‚ÇÇ] [Module ‚Ñö M] [Module ‚Ñö M‚ÇÇ] {F : Type*}
    [AddMonoidHomClass F M M‚ÇÇ] (f : F) (c : ‚Ñö) (x : M) : f (c ‚Ä¢ x) = c ‚Ä¢ f x :=
  map_rat_cast_smul f ‚Ñö ‚Ñö c x
#align map_rat_smul map_rat_smul

/-- There can be at most one `Module ‚Ñö E` structure on an additive commutative group. -/
instance subsingleton_rat_module (E : Type*) [AddCommGroup E] : Subsingleton (Module ‚Ñö E) :=
  ‚ü®fun P Q => (Module.ext' P Q) fun r x => @map_rat_smul _ _ _ _ P Q _ _ (AddMonoidHom.id E) r x‚ü©
#align subsingleton_rat_module subsingleton_rat_module

/-- If `E` is a vector space over two division semirings `R` and `S`, then scalar multiplications
agree on inverses of natural numbers in `R` and `S`. -/
theorem inv_nat_cast_smul_eq {E : Type*} (R S : Type*) [AddCommMonoid E] [DivisionSemiring R]
    [DivisionSemiring S] [Module R E] [Module S E] (n : ‚Ñï) (x : E) :
    (n‚Åª¬π : R) ‚Ä¢ x = (n‚Åª¬π : S) ‚Ä¢ x :=
  map_inv_nat_cast_smul (AddMonoidHom.id E) R S n x
#align inv_nat_cast_smul_eq inv_nat_cast_smul_eq

/-- If `E` is a vector space over two division rings `R` and `S`, then scalar multiplications
agree on inverses of integer numbers in `R` and `S`. -/
theorem inv_int_cast_smul_eq {E : Type*} (R S : Type*) [AddCommGroup E] [DivisionRing R]
    [DivisionRing S] [Module R E] [Module S E] (n : ‚Ñ§) (x : E) : (n‚Åª¬π : R) ‚Ä¢ x = (n‚Åª¬π : S) ‚Ä¢ x :=
  map_inv_int_cast_smul (AddMonoidHom.id E) R S n x
#align inv_int_cast_smul_eq inv_int_cast_smul_eq

/-- If `E` is a vector space over a division semiring `R` and has a monoid action by `Œ±`, then that
action commutes by scalar multiplication of inverses of natural numbers in `R`. -/
theorem inv_nat_cast_smul_comm {Œ± E : Type*} (R : Type*) [AddCommMonoid E] [DivisionSemiring R]
    [Monoid Œ±] [Module R E] [DistribMulAction Œ± E] (n : ‚Ñï) (s : Œ±) (x : E) :
    (n‚Åª¬π : R) ‚Ä¢ s ‚Ä¢ x = s ‚Ä¢ (n‚Åª¬π : R) ‚Ä¢ x :=
  (map_inv_nat_cast_smul (DistribMulAction.toAddMonoidHom E s) R R n x).symm
#align inv_nat_cast_smul_comm inv_nat_cast_smul_comm

/-- If `E` is a vector space over a division ring `R` and has a monoid action by `Œ±`, then that
action commutes by scalar multiplication of inverses of integers in `R` -/
theorem inv_int_cast_smul_comm {Œ± E : Type*} (R : Type*) [AddCommGroup E] [DivisionRing R]
    [Monoid Œ±] [Module R E] [DistribMulAction Œ± E] (n : ‚Ñ§) (s : Œ±) (x : E) :
    (n‚Åª¬π : R) ‚Ä¢ s ‚Ä¢ x = s ‚Ä¢ (n‚Åª¬π : R) ‚Ä¢ x :=
  (map_inv_int_cast_smul (DistribMulAction.toAddMonoidHom E s) R R n x).symm
#align inv_int_cast_smul_comm inv_int_cast_smul_comm

/-- If `E` is a vector space over two division rings `R` and `S`, then scalar multiplications
agree on rational numbers in `R` and `S`. -/
theorem rat_cast_smul_eq {E : Type*} (R S : Type*) [AddCommGroup E] [DivisionRing R]
    [DivisionRing S] [Module R E] [Module S E] (r : ‚Ñö) (x : E) : (r : R) ‚Ä¢ x = (r : S) ‚Ä¢ x :=
  map_rat_cast_smul (AddMonoidHom.id E) R S r x
#align rat_cast_smul_eq rat_cast_smul_eq

instance AddCommGroup.intIsScalarTower {R : Type u} {M : Type v} [Ring R] [AddCommGroup M]
    [Module R M] : IsScalarTower ‚Ñ§ R M where
  smul_assoc n x y := ((smulAddHom R M).flip y).map_zsmul x n
#align add_comm_group.int_is_scalar_tower AddCommGroup.intIsScalarTower

instance IsScalarTower.rat {R : Type u} {M : Type v} [Ring R] [AddCommGroup M] [Module R M]
    [Module ‚Ñö R] [Module ‚Ñö M] : IsScalarTower ‚Ñö R M where
  smul_assoc r x y := map_rat_smul ((smulAddHom R M).flip y) r x
#align is_scalar_tower.rat IsScalarTower.rat

instance SMulCommClass.rat {R : Type u} {M : Type v} [Semiring R] [AddCommGroup M] [Module R M]
    [Module ‚Ñö M] : SMulCommClass ‚Ñö R M where
  smul_comm r x y := (map_rat_smul (smulAddHom R M x) r y).symm
#align smul_comm_class.rat SMulCommClass.rat

instance SMulCommClass.rat' {R : Type u} {M : Type v} [Semiring R] [AddCommGroup M] [Module R M]
    [Module ‚Ñö M] : SMulCommClass R ‚Ñö M :=
  SMulCommClass.symm _ _ _
#align smul_comm_class.rat' SMulCommClass.rat'

section NoZeroSMulDivisors

/-! ### `NoZeroSMulDivisors`

This section defines the `NoZeroSMulDivisors` class, and includes some tests
for the vanishing of elements (especially in modules over division rings).
-/


/-- `NoZeroSMulDivisors R M` states that a scalar multiple is `0` only if either argument is `0`.
This is a version of saying that `M` is torsion free, without assuming `R` is zero-divisor free.

The main application of `NoZeroSMulDivisors R M`, when `M` is a module,
is the result `smul_eq_zero`: a scalar multiple is `0` iff either argument is `0`.

It is a generalization of the `NoZeroDivisors` class to heterogeneous multiplication.
-/
class NoZeroSMulDivisors (R M : Type*) [Zero R] [Zero M] [SMul R M] : Prop where
  /-- If scalar multiplication yields zero, either the scalar or the vector was zero. -/
  eq_zero_or_eq_zero_of_smul_eq_zero : ‚àÄ {c : R} {x : M}, c ‚Ä¢ x = 0 ‚Üí c = 0 ‚à® x = 0
#align no_zero_smul_divisors NoZeroSMulDivisors

export NoZeroSMulDivisors (eq_zero_or_eq_zero_of_smul_eq_zero)

/-- Pullback a `NoZeroSMulDivisors` instance along an injective function. -/
theorem Function.Injective.noZeroSMulDivisors {R M N : Type*} [Zero R] [Zero M] [Zero N]
    [SMul R M] [SMul R N] [NoZeroSMulDivisors R N] (f : M ‚Üí N) (hf : Function.Injective f)
    (h0 : f 0 = 0) (hs : ‚àÄ (c : R) (x : M), f (c ‚Ä¢ x) = c ‚Ä¢ f x) : NoZeroSMulDivisors R M :=
  ‚ü®fun {_ _} h =>
    Or.imp_right (@hf _ _) <| h0.symm ‚ñ∏ eq_zero_or_eq_zero_of_smul_eq_zero (by rw [‚Üê hs, h, h0])‚ü©
                                                                               -- üéâ no goals
#align function.injective.no_zero_smul_divisors Function.Injective.noZeroSMulDivisors

-- See note [lower instance priority]
instance (priority := 100) NoZeroDivisors.toNoZeroSMulDivisors [Zero R] [Mul R]
    [NoZeroDivisors R] : NoZeroSMulDivisors R R :=
  ‚ü®fun {_ _} => eq_zero_or_eq_zero_of_mul_eq_zero‚ü©
#align no_zero_divisors.to_no_zero_smul_divisors NoZeroDivisors.toNoZeroSMulDivisors

theorem smul_ne_zero [Zero R] [Zero M] [SMul R M] [NoZeroSMulDivisors R M] {c : R} {x : M}
    (hc : c ‚â† 0) (hx : x ‚â† 0) : c ‚Ä¢ x ‚â† 0 := fun h =>
  (eq_zero_or_eq_zero_of_smul_eq_zero h).elim hc hx
#align smul_ne_zero smul_ne_zero

section SMulWithZero

variable [Zero R] [Zero M] [SMulWithZero R M] [NoZeroSMulDivisors R M] {c : R} {x : M}

@[simp]
theorem smul_eq_zero : c ‚Ä¢ x = 0 ‚Üî c = 0 ‚à® x = 0 :=
  ‚ü®eq_zero_or_eq_zero_of_smul_eq_zero, fun h =>
    h.elim (fun h => h.symm ‚ñ∏ zero_smul R x) fun h => h.symm ‚ñ∏ smul_zero c‚ü©
#align smul_eq_zero smul_eq_zero

theorem smul_ne_zero_iff : c ‚Ä¢ x ‚â† 0 ‚Üî c ‚â† 0 ‚àß x ‚â† 0 := by rw [Ne.def, smul_eq_zero, not_or]
                                                           -- üéâ no goals
#align smul_ne_zero_iff smul_ne_zero_iff

end SMulWithZero

section Module

variable [Semiring R] [AddCommMonoid M] [Module R M]

section Nat

variable [NoZeroSMulDivisors R M] [CharZero R]
variable (R) (M)

--include R

theorem Nat.noZeroSMulDivisors : NoZeroSMulDivisors ‚Ñï M :=
  ‚ü®by
    intro c x
    -- ‚ä¢ c ‚Ä¢ x = 0 ‚Üí c = 0 ‚à® x = 0
    rw [nsmul_eq_smul_cast R, smul_eq_zero]
    -- ‚ä¢ ‚Üëc = 0 ‚à® x = 0 ‚Üí c = 0 ‚à® x = 0
    simp‚ü©
    -- üéâ no goals
#align nat.no_zero_smul_divisors Nat.noZeroSMulDivisors

-- Porting note: left-hand side never simplifies when using simp on itself
--@[simp]
theorem two_nsmul_eq_zero {v : M} : 2 ‚Ä¢ v = 0 ‚Üî v = 0 := by
  haveI := Nat.noZeroSMulDivisors R M
  -- ‚ä¢ 2 ‚Ä¢ v = 0 ‚Üî v = 0
  simp [smul_eq_zero]
  -- üéâ no goals
#align two_nsmul_eq_zero two_nsmul_eq_zero

end Nat

variable (R M)

/-- If `M` is an `R`-module with one and `M` has characteristic zero, then `R` has characteristic
zero as well. Usually `M` is an `R`-algebra. -/
theorem CharZero.of_module (M) [AddCommMonoidWithOne M] [CharZero M] [Module R M] : CharZero R := by
  refine' ‚ü®fun m n h => @Nat.cast_injective M _ _ _ _ _‚ü©
  -- ‚ä¢ ‚Üëm = ‚Üën
  rw [‚Üê nsmul_one, ‚Üê nsmul_one, nsmul_eq_smul_cast R m (1 : M), nsmul_eq_smul_cast R n (1 : M), h]
  -- üéâ no goals
#align char_zero.of_module CharZero.of_module

end Module

section AddCommGroup

-- `R` can still be a semiring here
variable [Semiring R] [AddCommGroup M] [Module R M]

section SMulInjective

variable (M)

theorem smul_right_injective [NoZeroSMulDivisors R M] {c : R} (hc : c ‚â† 0) :
    Function.Injective ((¬∑ ‚Ä¢ ¬∑) c : M ‚Üí M) :=
  (injective_iff_map_eq_zero (smulAddHom R M c)).2 fun _ ha => (smul_eq_zero.mp ha).resolve_left hc
#align smul_right_injective smul_right_injective

variable {M}

theorem smul_right_inj [NoZeroSMulDivisors R M] {c : R} (hc : c ‚â† 0) {x y : M} :
    c ‚Ä¢ x = c ‚Ä¢ y ‚Üî x = y :=
  (smul_right_injective M hc).eq_iff
#align smul_right_inj smul_right_inj

end SMulInjective

section Nat

variable [NoZeroSMulDivisors R M] [CharZero R]
variable (R M)
--include R

theorem self_eq_neg {v : M} : v = -v ‚Üî v = 0 := by
  rw [‚Üê two_nsmul_eq_zero R M, two_smul, add_eq_zero_iff_eq_neg]
  -- üéâ no goals
#align self_eq_neg self_eq_neg

theorem neg_eq_self {v : M} : -v = v ‚Üî v = 0 := by rw [eq_comm, self_eq_neg R M]
                                                   -- üéâ no goals
#align neg_eq_self neg_eq_self

theorem self_ne_neg {v : M} : v ‚â† -v ‚Üî v ‚â† 0 :=
  (self_eq_neg R M).not
#align self_ne_neg self_ne_neg

theorem neg_ne_self {v : M} : -v ‚â† v ‚Üî v ‚â† 0 :=
  (neg_eq_self R M).not
#align neg_ne_self neg_ne_self

end Nat

end AddCommGroup

section Module

variable [Ring R] [AddCommGroup M] [Module R M] [NoZeroSMulDivisors R M]

section SMulInjective

variable (R)

theorem smul_left_injective {x : M} (hx : x ‚â† 0) : Function.Injective fun c : R => c ‚Ä¢ x :=
  fun c d h =>
  sub_eq_zero.mp
    ((smul_eq_zero.mp
          (calc
            (c - d) ‚Ä¢ x = c ‚Ä¢ x - d ‚Ä¢ x := sub_smul c d x
            _ = 0 := sub_eq_zero.mpr h
            )).resolve_right
      hx)
#align smul_left_injective smul_left_injective

end SMulInjective

end Module

section GroupWithZero

variable [GroupWithZero R] [AddMonoid M] [DistribMulAction R M]

-- see note [lower instance priority]
/-- This instance applies to `DivisionSemiring`s, in particular `NNReal` and `NNRat`. -/
instance (priority := 100) GroupWithZero.toNoZeroSMulDivisors : NoZeroSMulDivisors R M :=
  ‚ü®fun {_ _} h => or_iff_not_imp_left.2 fun hc => (smul_eq_zero_iff_eq' hc).1 h‚ü©
#align group_with_zero.to_no_zero_smul_divisors GroupWithZero.toNoZeroSMulDivisors

end GroupWithZero

-- see note [lower instance priority]
instance (priority := 100) RatModule.noZeroSMulDivisors [AddCommGroup M] [Module ‚Ñö M] :
    NoZeroSMulDivisors ‚Ñ§ M :=
  ‚ü®fun {k} {x : M} h => by
    simpa only [zsmul_eq_smul_cast ‚Ñö k x, smul_eq_zero, Rat.zero_iff_num_zero] using h‚ü©
    -- üéâ no goals
  -- Porting note: old proof was:
  --‚ü®fun {k x} h => by simpa [zsmul_eq_smul_cast ‚Ñö k x] using h‚ü©
#align rat_module.no_zero_smul_divisors RatModule.noZeroSMulDivisors

end NoZeroSMulDivisors

-- Porting note: simp can prove this
--@[simp]
theorem Nat.smul_one_eq_coe {R : Type*} [Semiring R] (m : ‚Ñï) : m ‚Ä¢ (1 : R) = ‚Üëm := by
  rw [nsmul_eq_mul, mul_one]
  -- üéâ no goals
#align nat.smul_one_eq_coe Nat.smul_one_eq_coe

-- Porting note: simp can prove this
--@[simp]
theorem Int.smul_one_eq_coe {R : Type*} [Ring R] (m : ‚Ñ§) : m ‚Ä¢ (1 : R) = ‚Üëm := by
  rw [zsmul_eq_mul, mul_one]
  -- üéâ no goals
#align int.smul_one_eq_coe Int.smul_one_eq_coe

assert_not_exists Multiset
