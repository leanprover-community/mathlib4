/-
Copyright (c) 2024 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import Mathlib.RingTheory.GradedAlgebra.Homogeneous.Submodule
import Mathlib.Algebra.Module.GradedModule.Basic
import Mathlib.LinearAlgebra.Quotient.Basic

/-!
# Quotient of Graded Module by a Homogeneous Submodule

-/

open SetLike DirectSum

variable {Î¹A Î¹M ÏƒA ÏƒM A M : Type*}

variable [Ring A] [AddCommGroup M] [Module A M]

variable {ğ’œ : Î¹A â†’ ÏƒA} {â„³ : Î¹M â†’ ÏƒM}
variable [DecidableEq Î¹A] [AddMonoid Î¹A] [SetLike ÏƒA A] [AddSubgroupClass ÏƒA A] [GradedRing ğ’œ]
variable [DecidableEq Î¹M] [SetLike ÏƒM M] [AddSubgroupClass ÏƒM M] [Decomposition â„³]
variable [VAdd Î¹A Î¹M] [GradedSMul ğ’œ â„³]

namespace HomogeneousSubmodule

variable (p : HomogeneousSubmodule ğ’œ â„³)

/--
The addive group homomorphism from `Máµ¢ â§¸ p âˆ© Máµ¢` to `M â§¸ p`
-/
@[simps!]
def quotientGradingEmb (i : Î¹M) :
    (â„³ i : AddSubgroup M) â§¸ (AddSubgroup.addSubgroupOf p.toAddSubgroup _) â†’+
    M â§¸ p.toSubmodule :=
  QuotientAddGroup.map _ _ (â„³ i : AddSubgroup M).subtype <| le_refl _

/--
`M â§¸ p` is a graded module over `A` whose `i`-th degree part is `Máµ¢ â§¸ p âˆ© Máµ¢`
-/
def quotientGrading (i : Î¹M) : AddSubgroup (M â§¸ p.toSubmodule) :=
  (p.quotientGradingEmb i).range

/--
`M â§¸ p â‰ƒ â¨áµ¢ Máµ¢ â§¸ Máµ¢ âˆ© p` is given by `[x] â†¦ i â†¦ [xáµ¢]`. This is well-defined because `p` is
homogeneous.
-/
def quotientGrading.decomposeAux : M â†’+ â¨ i, p.quotientGrading i :=
AddMonoidHom.comp
  (DFinsupp.liftAddHom fun i â†¦
    { toFun := fun m â†¦ .of _ i
        âŸ¨p.toSubmodule.mkQ m.1, âŸ¨QuotientAddGroup.mk' _ m, by erw [QuotientAddGroup.map_mk']âŸ©âŸ©
      map_zero' := DFinsupp.ext fun j â†¦ by
        simp only [ZeroMemClass.coe_zero, map_zero, zero_apply]
        ext
        by_cases h : i = j
        Â· subst h
          simp only [of_eq_same, ZeroMemClass.coe_zero]
        Â· rw [of_eq_of_ne]; exact h
      map_add' := by
        rintro a b
        simp only [AddSubgroup.coe_add, map_add, Submodule.mkQ_apply]
        refine DFinsupp.ext fun j â†¦ ?_
        ext
        by_cases h : i = j
        Â· subst h
          simp only [of_eq_same, add_apply, AddSubmonoid.mk_add_mk]
          rfl
        Â· rw [of_eq_of_ne, DirectSum.add_apply, of_eq_of_ne, of_eq_of_ne, add_zero] <;> exact h
         })
  (DirectSum.decomposeAddEquiv â„³).toAddMonoidHom

lemma quotientGrading.le_decomposeAux_ker :
    p.toSubmodule.toAddSubgroup â‰¤ (quotientGrading.decomposeAux p).ker := fun x hx â†¦
  show DFinsupp.sumAddHom _ (decompose â„³ x) = 0 by
  classical
  rw [show decompose â„³ x =
      âˆ‘ i in (decompose â„³ x).support,
        (DFinsupp.single i âŸ¨decompose â„³ x i, SetLike.coe_mem _âŸ©) from
      DFinsupp.ext fun i â†¦ by aesop, map_sum]
  simp_rw [DFinsupp.sumAddHom_single]
  refine Finset.sum_eq_zero fun i _ â†¦ DFinsupp.ext fun j â†¦ Subtype.ext <| show _ = 0 from ?_
  by_cases h : i = j
  Â· subst h; simpa [of_eq_same, Submodule.Quotient.mk_eq_zero, mem_iff] using p.2 _ hx
  Â· simp [of_eq_of_ne (h := h)]

/--
`M â§¸ p â‰ƒ â¨áµ¢ Máµ¢ â§¸ Máµ¢ âˆ© p` is given by `[x] â†¦ i â†¦ [xáµ¢]`. This is well-defined because `p` is
homogeneous.
-/
def quotientGradingDecompose : M â§¸ p.toSubmodule â†’+ â¨ i, p.quotientGrading i :=
QuotientAddGroup.lift _ (quotientGrading.decomposeAux p) (quotientGrading.le_decomposeAux_ker p)

lemma quotientGradingDecompose_apply_mkQ_of_mem (i : Î¹M) (m : M) (hm : m âˆˆ â„³ i) :
    p.quotientGradingDecompose (p.toSubmodule.mkQ m) i =
    p.toSubmodule.mkQ m := by
  classical
  simp only [quotientGradingDecompose, Submodule.mkQ_apply]
  erw [QuotientAddGroup.lift_mk']
  simp only [quotientGrading.decomposeAux, Submodule.mkQ_apply, DFinsupp.liftAddHom_apply,
    AddMonoidHom.coe_comp, Function.comp_apply]
  erw [show decompose â„³ m =
      âˆ‘ i in (decompose â„³ m).support,
        (DFinsupp.single i âŸ¨decompose â„³ m i, SetLike.coe_mem _âŸ©) from
      DFinsupp.ext fun i â†¦ by aesop, map_sum]

  simp only [Subtype.coe_eta, DFinsupp.sumAddHom_single, AddMonoidHom.coe_mk, ZeroHom.coe_mk]
  rw [DFinsupp.finset_sum_apply]
  conv_rhs => rw [â† DirectSum.sum_support_decompose â„³ m]
  change (p.quotientGrading i).subtype _ = p.toSubmodule.mkQ _
  rw [map_sum, map_sum]
  refine Finset.sum_congr rfl fun j hj â†¦ ?_
  simp only [AddSubgroup.coeSubtype, Submodule.mkQ_apply]
  by_cases h : i = j
  Â· subst h; simp only [of_eq_same]
  Â· rw [of_eq_of_ne (h := Ne.symm h), decompose_of_mem_ne (hx := hm) (hij := h),
      ZeroMemClass.coe_zero]; rfl

lemma quotientGradingDecompose_apply_mkQ_of_ne (i j : Î¹M) (m : M) (hm : m âˆˆ â„³ i) (h : i â‰  j) :
    p.quotientGradingDecompose (p.toSubmodule.mkQ m) j = 0 := by
  classical
  simp only [quotientGradingDecompose, Submodule.mkQ_apply]
  erw [QuotientAddGroup.lift_mk']
  simp only [quotientGrading.decomposeAux, Submodule.mkQ_apply, DFinsupp.liftAddHom_apply,
    AddMonoidHom.coe_comp, Function.comp_apply]
  erw [show decompose â„³ m =
      âˆ‘ i in (decompose â„³ m).support,
        (DFinsupp.single i âŸ¨decompose â„³ m i, SetLike.coe_mem _âŸ©) from
      DFinsupp.ext fun i â†¦ by aesop, map_sum]
  simp only [Subtype.coe_eta, DFinsupp.sumAddHom_single, AddMonoidHom.coe_mk, ZeroHom.coe_mk]
  rw [DFinsupp.finset_sum_apply]
  refine Finset.sum_eq_zero fun k hk â†¦ ?_
  ext
  by_cases h : j = k
  Â· subst h
    simp only [of_eq_same, ZeroMemClass.coe_zero, Submodule.Quotient.mk_eq_zero, mem_iff]
    rw [decompose_of_mem_ne (hx := hm) (hij := h)]
    exact p.toSubmodule.zero_mem
  Â· rw [of_eq_of_ne]; exact Ne.symm h

lemma quotientGradingDecompose_leftInverse :
    Function.LeftInverse (DirectSum.coeAddMonoidHom p.quotientGrading)
      p.quotientGradingDecompose := by
  classical
  intro x
  induction x using Quotient.inductionOn' with | h x => ?_
  simp only [quotientGradingDecompose, quotientGrading.decomposeAux, Submodule.mkQ_apply,
    DFinsupp.liftAddHom_apply]
  erw [QuotientAddGroup.lift_mk']
  change DirectSum.coeAddMonoidHom _ (DFinsupp.sumAddHom _ (decompose â„³ x)) = _
  erw [show decompose â„³ x =
      âˆ‘ i in (decompose â„³ x).support,
        (DFinsupp.single i âŸ¨decompose â„³ x i, SetLike.coe_mem _âŸ©) from
      DFinsupp.ext fun i â†¦ by aesop, map_sum]
  simp_rw [DFinsupp.sumAddHom_single]
  rw [map_sum]
  simp only [Subtype.coe_eta, AddMonoidHom.coe_mk, ZeroHom.coe_mk, coeAddMonoidHom_of,
    Submodule.Quotient.mk''_eq_mk]
  conv_rhs => rw [â† DirectSum.sum_support_decompose â„³ x]
  change _ = p.toSubmodule.mkQ _
  rw [map_sum]
  rfl

lemma quotientGradingDecompose_rightInverse :
    Function.RightInverse (DirectSum.coeAddMonoidHom p.quotientGrading)
      p.quotientGradingDecompose :=
  fun x â†¦ DFinsupp.ext fun i â†¦ by
  induction x using DirectSum.induction_on with
  | H_zero => simp
  | H_basic j x =>
    rcases x with âŸ¨x, hxâŸ©
    induction x using Quotient.inductionOn' with | h x =>
    obtain âŸ¨y, hyâŸ© := hx
    induction y using Quotient.inductionOn' with | h y =>
    erw [quotientGradingEmb, QuotientAddGroup.map_mk'] at hy
    rcases y with âŸ¨y, hy'âŸ©
    simp only [AddSubgroup.coeSubtype] at hy
    simp_rw [â† hy]
    simp only [coeAddMonoidHom_of]
    by_cases h : i = j
    Â· ext
      subst h
      simp only [of_eq_same]
      exact quotientGradingDecompose_apply_mkQ_of_mem p i y hy'
    Â· ext
      rw [of_eq_of_ne (h := Ne.symm h), ZeroMemClass.coe_zero, ZeroMemClass.coe_eq_zero]
      exact quotientGradingDecompose_apply_mkQ_of_ne p j i y hy' (Ne.symm h)
  | H_plus _ _ h h' => simp [h, h']

instance quotientDecomposition : DirectSum.Decomposition p.quotientGrading where
  decompose' := p.quotientGradingDecompose
  left_inv := p.quotientGradingDecompose_leftInverse
  right_inv := p.quotientGradingDecompose_rightInverse

instance quotientGradedSMul : SetLike.GradedSMul ğ’œ p.quotientGrading where
  smul_mem := by
    intro i j a m ha hm
    obtain âŸ¨x, rflâŸ© := hm
    induction x using Quotient.inductionOn' with | h x => ?_
    rcases x with âŸ¨x, hxâŸ©
    exact âŸ¨Quotient.mk'' âŸ¨a â€¢ x, (inferInstance : SetLike.GradedSMul ğ’œ â„³).smul_mem ha hxâŸ©, rflâŸ©

end HomogeneousSubmodule
