/-
Copyright (c) 2023 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.LinearAlgebra.FreeModule.PID
import Mathlib.MeasureTheory.Group.FundamentalDomain
import Mathlib.RingTheory.Localization.Module

#align_import algebra.module.zlattice from "leanprover-community/mathlib"@"a3e83f0fa4391c8740f7d773a7a9b74e311ae2a3"

/-!
# â„¤-lattices

Let `E` be a finite dimensional vector space over a `NormedLinearOrderedField` `K` with a solid
norm that is also a `FloorRing`, e.g. `â„`. A (full) `â„¤`-lattice `L` of `E` is a discrete
subgroup of `E` such that `L` spans `E` over `K`.

A `â„¤`-lattice `L` can be defined in two ways:
* For `b` a basis of `E`, then `L = Submodule.span â„¤ (Set.range b)` is a â„¤-lattice of `E`
* As an `AddSubgroup E` with the additional properties:
  * `DiscreteTopology L`, that is `L` is discrete
  * `Submodule.span â„ (L : Set E) = âŠ¤`, that is `L` spans `E` over `K`.

Results about the first point of view are in the `Zspan` namespace and results about the second
point of view are in the `Zlattice` namespace.

## Main results

* `Zspan.isAddFundamentalDomain`: for a â„¤-lattice `Submodule.span â„¤ (Set.range b)`, proves that
the set defined by `Zspan.fundamentalDomain` is a fundamental domain.
* `Zlattice.module_free`: an AddSubgroup of `E` that is discrete and spans `E` over `K` is a free
`â„¤`-module
* `Zlattice.rank`:  an AddSubgroup of `E` that is discrete and spans `E` over `K` is a free
`â„¤`-module of `â„¤`-rank equal to the `K`-rank of `E`
-/


open scoped BigOperators

noncomputable section

namespace Zspan

open MeasureTheory MeasurableSet Submodule

variable {E Î¹ : Type*}

section NormedLatticeField

variable {K : Type*} [NormedLinearOrderedField K]

variable [NormedAddCommGroup E] [NormedSpace K E]

variable (b : Basis Î¹ K E)

/-- The fundamental domain of the â„¤-lattice spanned by `b`. See `Zspan.isAddFundamentalDomain`
for the proof that it is a fundamental domain. -/
def fundamentalDomain : Set E := {m | âˆ€ i, b.repr m i âˆˆ Set.Ico (0 : K) 1}
#align zspan.fundamental_domain Zspan.fundamentalDomain

@[simp]
theorem mem_fundamentalDomain {m : E} :
    m âˆˆ fundamentalDomain b â†” âˆ€ i, b.repr m i âˆˆ Set.Ico (0 : K) 1 := Iff.rfl
#align zspan.mem_fundamental_domain Zspan.mem_fundamentalDomain

variable [FloorRing K]

section Fintype

variable [Fintype Î¹]

/-- The map that sends a vector of `E` to the element of the â„¤-lattice spanned by `b` obtained
by rounding down its coordinates on the basis `b`. -/
def floor (m : E) : span â„¤ (Set.range b) := âˆ‘ i, âŒŠb.repr m iâŒ‹ â€¢ b.restrictScalars â„¤ i
#align zspan.floor Zspan.floor

/-- The map that sends a vector of `E` to the element of the â„¤-lattice spanned by `b` obtained
by rounding up its coordinates on the basis `b`. -/
def ceil (m : E) : span â„¤ (Set.range b) := âˆ‘ i, âŒˆb.repr m iâŒ‰ â€¢ b.restrictScalars â„¤ i
#align zspan.ceil Zspan.ceil

@[simp]
theorem repr_floor_apply (m : E) (i : Î¹) : b.repr (floor b m) i = âŒŠb.repr m iâŒ‹ := by
  classical simp only [floor, zsmul_eq_smul_cast K, b.repr.map_smul, Finsupp.single_apply,
    Finset.sum_apply', Basis.repr_self, Finsupp.smul_single', mul_one, Finset.sum_ite_eq', coe_sum,
    Finset.mem_univ, if_true, coe_smul_of_tower, Basis.restrictScalars_apply, LinearEquiv.map_sum]
#align zspan.repr_floor_apply Zspan.repr_floor_apply

@[simp]
theorem repr_ceil_apply (m : E) (i : Î¹) : b.repr (ceil b m) i = âŒˆb.repr m iâŒ‰ := by
  classical simp only [ceil, zsmul_eq_smul_cast K, b.repr.map_smul, Finsupp.single_apply,
    Finset.sum_apply', Basis.repr_self, Finsupp.smul_single', mul_one, Finset.sum_ite_eq', coe_sum,
    Finset.mem_univ, if_true, coe_smul_of_tower, Basis.restrictScalars_apply, LinearEquiv.map_sum]
#align zspan.repr_ceil_apply Zspan.repr_ceil_apply

@[simp]
theorem floor_eq_self_of_mem (m : E) (h : m âˆˆ span â„¤ (Set.range b)) : (floor b m : E) = m := by
  apply b.ext_elem
  -- âŠ¢ âˆ€ (i : Î¹), â†‘(â†‘b.repr â†‘(floor b m)) i = â†‘(â†‘b.repr m) i
  simp_rw [repr_floor_apply b]
  -- âŠ¢ âˆ€ (i : Î¹), â†‘âŒŠâ†‘(â†‘b.repr m) iâŒ‹ = â†‘(â†‘b.repr m) i
  intro i
  -- âŠ¢ â†‘âŒŠâ†‘(â†‘b.repr m) iâŒ‹ = â†‘(â†‘b.repr m) i
  obtain âŸ¨z, hzâŸ© := (b.mem_span_iff_repr_mem â„¤ _).mp h i
  -- âŠ¢ â†‘âŒŠâ†‘(â†‘b.repr m) iâŒ‹ = â†‘(â†‘b.repr m) i
  rw [â† hz]
  -- âŠ¢ â†‘âŒŠâ†‘(algebraMap â„¤ K) zâŒ‹ = â†‘(algebraMap â„¤ K) z
  exact congr_arg (Int.cast : â„¤ â†’ K) (Int.floor_intCast z)
  -- ğŸ‰ no goals
#align zspan.floor_eq_self_of_mem Zspan.floor_eq_self_of_mem

@[simp]
theorem ceil_eq_self_of_mem (m : E) (h : m âˆˆ span â„¤ (Set.range b)) : (ceil b m : E) = m := by
  apply b.ext_elem
  -- âŠ¢ âˆ€ (i : Î¹), â†‘(â†‘b.repr â†‘(ceil b m)) i = â†‘(â†‘b.repr m) i
  simp_rw [repr_ceil_apply b]
  -- âŠ¢ âˆ€ (i : Î¹), â†‘âŒˆâ†‘(â†‘b.repr m) iâŒ‰ = â†‘(â†‘b.repr m) i
  intro i
  -- âŠ¢ â†‘âŒˆâ†‘(â†‘b.repr m) iâŒ‰ = â†‘(â†‘b.repr m) i
  obtain âŸ¨z, hzâŸ© := (b.mem_span_iff_repr_mem â„¤ _).mp h i
  -- âŠ¢ â†‘âŒˆâ†‘(â†‘b.repr m) iâŒ‰ = â†‘(â†‘b.repr m) i
  rw [â† hz]
  -- âŠ¢ â†‘âŒˆâ†‘(algebraMap â„¤ K) zâŒ‰ = â†‘(algebraMap â„¤ K) z
  exact congr_arg (Int.cast : â„¤ â†’ K) (Int.ceil_intCast z)
  -- ğŸ‰ no goals
#align zspan.ceil_eq_self_of_mem Zspan.ceil_eq_self_of_mem

/-- The map that sends a vector `E` to the `fundamentalDomain` of the lattice,
see `Zspan.fract_mem_fundamentalDomain`, and `fract_restrict` for the map with the codomain
restricted to `fundamentalDomain`. -/
def fract (m : E) : E := m - floor b m
#align zspan.fract Zspan.fract

theorem fract_apply (m : E) : fract b m = m - floor b m := rfl
#align zspan.fract_apply Zspan.fract_apply

@[simp]
theorem repr_fract_apply (m : E) (i : Î¹) : b.repr (fract b m) i = Int.fract (b.repr m i) := by
  rw [fract, LinearEquiv.map_sub, Finsupp.coe_sub, Pi.sub_apply, repr_floor_apply, Int.fract]
  -- ğŸ‰ no goals
#align zspan.repr_fract_apply Zspan.repr_fract_apply

@[simp]
theorem fract_fract (m : E) : fract b (fract b m) = fract b m :=
  Basis.ext_elem b fun _ => by classical simp only [repr_fract_apply, Int.fract_fract]
                               -- ğŸ‰ no goals
#align zspan.fract_fract Zspan.fract_fract

@[simp]
theorem fract_zspan_add (m : E) {v : E} (h : v âˆˆ span â„¤ (Set.range b)) :
    fract b (v + m) = fract b m := by
  classical
  refine (Basis.ext_elem_iff b).mpr fun i => ?_
  simp_rw [repr_fract_apply, Int.fract_eq_fract]
  use (b.restrictScalars â„¤).repr âŸ¨v, hâŸ© i
  rw [map_add, Finsupp.coe_add, Pi.add_apply, add_tsub_cancel_right,
    â† eq_intCast (algebraMap â„¤ K) _, Basis.restrictScalars_repr_apply, coe_mk]
#align zspan.fract_zspan_add Zspan.fract_zspan_add

@[simp]
theorem fract_add_zspan (m : E) {v : E} (h : v âˆˆ span â„¤ (Set.range b)) :
    fract b (m + v) = fract b m := by rw [add_comm, fract_zspan_add b m h]
                                      -- ğŸ‰ no goals
#align zspan.fract_add_zspan Zspan.fract_add_zspan

variable {b}

theorem fract_eq_self {x : E} : fract b x = x â†” x âˆˆ fundamentalDomain b := by
  classical simp only [Basis.ext_elem_iff b, repr_fract_apply, Int.fract_eq_self,
    mem_fundamentalDomain, Set.mem_Ico]
#align zspan.fract_eq_self Zspan.fract_eq_self

variable (b)

theorem fract_mem_fundamentalDomain (x : E) : fract b x âˆˆ fundamentalDomain b :=
  fract_eq_self.mp (fract_fract b _)
#align zspan.fract_mem_fundamental_domain Zspan.fract_mem_fundamentalDomain

/-- The map `fract` with codomain restricted to `fundamentalDomain`. -/
def fract_restrict (x : E) : fundamentalDomain b := âŸ¨fract b x, fract_mem_fundamentalDomain b xâŸ©

theorem fract_restrict_surjective : Function.Surjective (fract_restrict b) :=
  fun x => âŸ¨â†‘x, Subtype.eq (fract_eq_self.mpr (Subtype.mem x))âŸ©

@[simp]
theorem fract_restrict_apply (x : E) : (fract_restrict b x : E) = fract b x := rfl

theorem fract_eq_fract (m n : E) : fract b m = fract b n â†” -m + n âˆˆ span â„¤ (Set.range b) := by
  classical
  rw [eq_comm, Basis.ext_elem_iff b]
  simp_rw [repr_fract_apply, Int.fract_eq_fract, eq_comm, Basis.mem_span_iff_repr_mem,
    sub_eq_neg_add, map_add, LinearEquiv.map_neg, Finsupp.coe_add, Finsupp.coe_neg, Pi.add_apply,
    Pi.neg_apply, â† eq_intCast (algebraMap â„¤ K) _, Set.mem_range]
#align zspan.fract_eq_fract Zspan.fract_eq_fract

theorem norm_fract_le [HasSolidNorm K] (m : E) : â€–fract b mâ€– â‰¤ âˆ‘ i, â€–b iâ€– := by
  classical
  calc
    â€–fract b mâ€– = â€–âˆ‘ i, b.repr (fract b m) i â€¢ b iâ€– := by rw [b.sum_repr]
    _ = â€–âˆ‘ i, Int.fract (b.repr m i) â€¢ b iâ€– := by simp_rw [repr_fract_apply]
    _ â‰¤ âˆ‘ i, â€–Int.fract (b.repr m i) â€¢ b iâ€– := (norm_sum_le _ _)
    _ = âˆ‘ i, â€–Int.fract (b.repr m i)â€– * â€–b iâ€– := by simp_rw [norm_smul]
    _ â‰¤ âˆ‘ i, â€–b iâ€– := Finset.sum_le_sum fun i _ => ?_
  suffices â€–Int.fract ((b.repr m) i)â€– â‰¤ 1 by
    convert mul_le_mul_of_nonneg_right this (norm_nonneg _ : 0 â‰¤ â€–b iâ€–)
    exact (one_mul _).symm
  rw [(norm_one.symm : 1 = â€–(1 : K)â€–)]
  apply norm_le_norm_of_abs_le_abs
  rw [abs_one, Int.abs_fract]
  exact le_of_lt (Int.fract_lt_one _)
#align zspan.norm_fract_le Zspan.norm_fract_le

section Unique

variable [Unique Î¹]

@[simp]
theorem coe_floor_self (k : K) : (floor (Basis.singleton Î¹ K) k : K) = âŒŠkâŒ‹ :=
  Basis.ext_elem _ fun _ => by rw [repr_floor_apply, Basis.singleton_repr, Basis.singleton_repr]
                               -- ğŸ‰ no goals
#align zspan.coe_floor_self Zspan.coe_floor_self

@[simp]
theorem coe_fract_self (k : K) : (fract (Basis.singleton Î¹ K) k : K) = Int.fract k :=
  Basis.ext_elem _ fun _ => by rw [repr_fract_apply, Basis.singleton_repr, Basis.singleton_repr]
                               -- ğŸ‰ no goals
#align zspan.coe_fract_self Zspan.coe_fract_self

end Unique

end Fintype

theorem fundamentalDomain_bounded [Finite Î¹] [HasSolidNorm K] :
    Metric.Bounded (fundamentalDomain b) := by
  cases nonempty_fintype Î¹
  -- âŠ¢ Metric.Bounded (fundamentalDomain b)
  use 2 * âˆ‘ j, â€–b jâ€–
  -- âŠ¢ âˆ€ (x : E), x âˆˆ fundamentalDomain b â†’ âˆ€ (y : E), y âˆˆ fundamentalDomain b â†’ di â€¦
  intro x hx y hy
  -- âŠ¢ dist x y â‰¤ 2 * âˆ‘ j : Î¹, â€–â†‘b jâ€–
  refine le_trans (dist_le_norm_add_norm x y) ?_
  -- âŠ¢ â€–xâ€– + â€–yâ€– â‰¤ 2 * âˆ‘ j : Î¹, â€–â†‘b jâ€–
  rw [â† fract_eq_self.mpr hx, â† fract_eq_self.mpr hy]
  -- âŠ¢ â€–fract b xâ€– + â€–fract b yâ€– â‰¤ 2 * âˆ‘ j : Î¹, â€–â†‘b jâ€–
  refine (add_le_add (norm_fract_le b x) (norm_fract_le b y)).trans ?_
  -- âŠ¢ âˆ‘ i : Î¹, â€–â†‘b iâ€– + âˆ‘ i : Î¹, â€–â†‘b iâ€– â‰¤ 2 * âˆ‘ j : Î¹, â€–â†‘b jâ€–
  rw [â† two_mul]
  -- ğŸ‰ no goals
#align zspan.fundamental_domain_bounded Zspan.fundamentalDomain_bounded

theorem vadd_mem_fundamentalDomain [Fintype Î¹] (y : span â„¤ (Set.range b)) (x : E) :
    y +áµ¥ x âˆˆ fundamentalDomain b â†” y = -floor b x := by
  rw [Subtype.ext_iff, â† add_right_inj x, AddSubgroupClass.coe_neg, â† sub_eq_add_neg, â† fract_apply,
    â† fract_zspan_add b _ (Subtype.mem y), add_comm, â† vadd_eq_add, â† vadd_def, eq_comm, â†
    fract_eq_self]
#align zspan.vadd_mem_fundamental_domain Zspan.vadd_mem_fundamentalDomain

theorem exist_unique_vadd_mem_fundamentalDomain [Finite Î¹] (x : E) :
    âˆƒ! v : span â„¤ (Set.range b), v +áµ¥ x âˆˆ fundamentalDomain b := by
  cases nonempty_fintype Î¹
  -- âŠ¢ âˆƒ! v, v +áµ¥ x âˆˆ fundamentalDomain b
  refine âŸ¨-floor b x, ?_, fun y h => ?_âŸ©
  -- âŠ¢ (fun v => v +áµ¥ x âˆˆ fundamentalDomain b) (-floor b x)
  Â· exact (vadd_mem_fundamentalDomain b (-floor b x) x).mpr rfl
    -- ğŸ‰ no goals
  Â· exact (vadd_mem_fundamentalDomain b y x).mp h
    -- ğŸ‰ no goals
#align zspan.exist_unique_vadd_mem_fundamental_domain Zspan.exist_unique_vadd_mem_fundamentalDomain

/-- The map `Zspan.fract_restrict` defines an equiv map between `E â§¸ span â„¤ (Set.range b)`
and `Zspan.fundamentalDomain b`. -/
def QuotientEquiv [Fintype Î¹] :
    E â§¸ span â„¤ (Set.range b) â‰ƒ (fundamentalDomain b) := by
  refine Equiv.ofBijective ?_ âŸ¨fun x y => ?_, fun x => ?_âŸ©
  Â· refine fun q => Quotient.liftOn q (fract_restrict b) (fun _ _ h => ?_)
    -- âŠ¢ fract_restrict b xâœÂ¹ = fract_restrict b xâœ
    rw [Subtype.mk.injEq, fract_restrict_apply, fract_restrict_apply, fract_eq_fract]
    -- âŠ¢ -xâœÂ¹ + xâœ âˆˆ span â„¤ (Set.range â†‘b)
    exact QuotientAddGroup.leftRel_apply.mp h
    -- ğŸ‰ no goals
  Â· refine Quotient.inductionOnâ‚‚ x y (fun _ _ hxy => ?_)
    -- âŠ¢ Quotient.mk (quotientRel (span â„¤ (Set.range â†‘b))) xâœÂ¹ = Quotient.mk (quotien â€¦
    rw [Quotient.liftOn_mk (s := quotientRel (span â„¤ (Set.range b))), fract_restrict,
      Quotient.liftOn_mk (s := quotientRel (span â„¤ (Set.range b))),  fract_restrict,
      Subtype.mk.injEq] at hxy
    apply Quotient.sound'
    -- âŠ¢ Setoid.r xâœÂ¹ xâœ
    rwa [QuotientAddGroup.leftRel_apply, mem_toAddSubgroup, â† fract_eq_fract]
    -- ğŸ‰ no goals
  Â· obtain âŸ¨a, rflâŸ© := fract_restrict_surjective b x
    -- âŠ¢ âˆƒ a_1, Quotient.liftOn a_1 (fract_restrict b) (_ : âˆ€ (x x_1 : E), x â‰ˆ x_1 â†’  â€¦
    exact âŸ¨Quotient.mk'' a, rflâŸ©
    -- ğŸ‰ no goals

@[simp]
theorem quotientEquiv_apply_mk [Fintype Î¹] (x : E) :
    QuotientEquiv b (Submodule.Quotient.mk x) = fract_restrict b x := rfl

@[simp]
theorem quotientEquiv.symm_apply [Fintype Î¹] (x : fundamentalDomain b) :
    (QuotientEquiv b).symm x = Submodule.Quotient.mk â†‘x := by
  rw [Equiv.symm_apply_eq, quotientEquiv_apply_mk b â†‘x, Subtype.ext_iff, fract_restrict_apply]
  -- âŠ¢ â†‘x = fract b â†‘x
  exact (fract_eq_self.mpr x.prop).symm
  -- ğŸ‰ no goals

end NormedLatticeField

section Real

variable [NormedAddCommGroup E] [NormedSpace â„ E]

variable (b : Basis Î¹ â„ E)

@[measurability]
theorem fundamentalDomain_measurableSet [MeasurableSpace E] [OpensMeasurableSpace E] [Finite Î¹] :
    MeasurableSet (fundamentalDomain b) := by
  haveI : FiniteDimensional â„ E := FiniteDimensional.of_fintype_basis b
  -- âŠ¢ MeasurableSet (fundamentalDomain b)
  let f := (Finsupp.linearEquivFunOnFinite â„ â„ Î¹).toLinearMap.comp b.repr.toLinearMap
  -- âŠ¢ MeasurableSet (fundamentalDomain b)
  let D : Set (Î¹ â†’ â„) := Set.pi Set.univ fun _ : Î¹ => Set.Ico (0 : â„) 1
  -- âŠ¢ MeasurableSet (fundamentalDomain b)
  rw [(_ : fundamentalDomain b = f â»Â¹' D)]
  -- âŠ¢ MeasurableSet (â†‘f â»Â¹' D)
  Â· refine measurableSet_preimage (LinearMap.continuous_of_finiteDimensional f).measurable ?_
    -- âŠ¢ MeasurableSet D
    exact MeasurableSet.pi Set.countable_univ fun _ _ => measurableSet_Ico
    -- ğŸ‰ no goals
  Â· ext
    -- âŠ¢ xâœ âˆˆ fundamentalDomain b â†” xâœ âˆˆ â†‘f â»Â¹' D
    simp only [fundamentalDomain, Set.mem_setOf_eq, LinearMap.coe_comp,
      LinearEquiv.coe_toLinearMap, Set.mem_preimage, Function.comp_apply, Set.mem_univ_pi,
      Finsupp.linearEquivFunOnFinite_apply]
#align zspan.fundamental_domain_measurable_set Zspan.fundamentalDomain_measurableSet

/-- For a â„¤-lattice `Submodule.span â„¤ (Set.range b)`, proves that the set defined
by `Zspan.fundamentalDomain` is a fundamental domain. -/
protected theorem isAddFundamentalDomain [Finite Î¹] [MeasurableSpace E] [OpensMeasurableSpace E]
    (Î¼ : Measure E) :
    IsAddFundamentalDomain (span â„¤ (Set.range b)).toAddSubgroup (fundamentalDomain b) Î¼ := by
  cases nonempty_fintype Î¹
  -- âŠ¢ IsAddFundamentalDomain { x // x âˆˆ toAddSubgroup (span â„¤ (Set.range â†‘b)) } (f â€¦
  exact IsAddFundamentalDomain.mk' (nullMeasurableSet (fundamentalDomain_measurableSet b))
    fun x => exist_unique_vadd_mem_fundamentalDomain b x
#align zspan.is_add_fundamental_domain Zspan.isAddFundamentalDomain

end Real

end Zspan

section Zlattice

open Submodule

variable (K : Type*) [NormedLinearOrderedField K] [HasSolidNorm K] [FloorRing K]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace K E] [FiniteDimensional K E]
variable [ProperSpace E] {L : AddSubgroup E} [DiscreteTopology L]
variable (hs : span K (L : Set E) = âŠ¤)

theorem Zlattice.FG : AddSubgroup.FG L := by
  suffices (AddSubgroup.toIntSubmodule L).FG by exact (fg_iff_add_subgroup_fg _).mp this
  -- âŠ¢ Submodule.FG (â†‘AddSubgroup.toIntSubmodule L)
  obtain âŸ¨s, âŸ¨h_incl, âŸ¨h_span, h_lindâŸ©âŸ©âŸ© := exists_linearIndependent K (L : Set E)
  -- âŠ¢ Submodule.FG (â†‘AddSubgroup.toIntSubmodule L)
  -- Let `s` be a maximal `K`-linear independent family of elements of `L`. We show that
  -- `L` is finitely generated (as a â„¤-module) because it fits in the exact sequence
  -- `0 â†’ span â„¤ s â†’ L â†’ L â§¸ span â„¤ s â†’ 0` with `span â„¤ s` and `L â§¸ span â„¤ s` finitely generated.
  refine fg_of_fg_map_of_fg_inf_ker (span â„¤ s).mkQ ?_ ?_
  -- âŠ¢ Submodule.FG (map (mkQ (span â„¤ s)) (â†‘AddSubgroup.toIntSubmodule L))
  Â· -- Let `b` be the `K`-basis of `E` formed by the vectors in `s`. The elements of
    -- `L â§¸ span â„¤ s = L â§¸ span â„¤ b` are in bijection with elements of `L âˆ© fundamentalDomain b`
    -- so there are finitely many since `fundamentalDomain b` is bounded.
    refine fg_def.mpr âŸ¨map (span â„¤ s).mkQ (AddSubgroup.toIntSubmodule L), ?_, span_eq _âŸ©
    -- âŠ¢ Set.Finite â†‘(map (mkQ (span â„¤ s)) (â†‘AddSubgroup.toIntSubmodule L))
    let b := Basis.mk h_lind (by
      rw [â† hs, â† h_span]
      exact span_mono (by simp only [Subtype.range_coe_subtype, Set.setOf_mem_eq, subset_rfl]))
    rw [show span â„¤ s = span â„¤ (Set.range b) by simp [Basis.coe_mk, Subtype.range_coe_subtype]]
    -- âŠ¢ Set.Finite â†‘(map (mkQ (span â„¤ (Set.range â†‘b))) (â†‘AddSubgroup.toIntSubmodule  â€¦
    have : Fintype s := Set.Finite.fintype h_lind.finite
    -- âŠ¢ Set.Finite â†‘(map (mkQ (span â„¤ (Set.range â†‘b))) (â†‘AddSubgroup.toIntSubmodule  â€¦
    refine Set.Finite.of_finite_image (f := ((â†‘) : _ â†’  E) âˆ˜ Zspan.QuotientEquiv b) ?_
      (Function.Injective.injOn (Subtype.coe_injective.comp (Zspan.QuotientEquiv b).injective) _)
    have : Set.Finite ((Zspan.fundamentalDomain b) âˆ© L) :=
      Metric.Finite_bounded_inter_isClosed (Zspan.fundamentalDomain_bounded b) inferInstance
    refine Set.Finite.subset this ?_
    -- âŠ¢ Subtype.val âˆ˜ â†‘(Zspan.QuotientEquiv b) '' â†‘(map (mkQ (span â„¤ (Set.range â†‘b)) â€¦
    rintro _ âŸ¨_, âŸ¨âŸ¨x, âŸ¨h_mem, rflâŸ©âŸ©, rflâŸ©âŸ©
    -- âŠ¢ (Subtype.val âˆ˜ â†‘(Zspan.QuotientEquiv b)) (â†‘(mkQ (span â„¤ (Set.range â†‘b))) x)  â€¦
    rw [Function.comp_apply, mkQ_apply, Zspan.quotientEquiv_apply_mk, Zspan.fract_restrict_apply]
    -- âŠ¢ Zspan.fract b x âˆˆ Zspan.fundamentalDomain b âˆ© â†‘L
    refine âŸ¨?_, ?_âŸ©
    -- âŠ¢ Zspan.fract b x âˆˆ Zspan.fundamentalDomain b
    Â· exact Zspan.fract_mem_fundamentalDomain b x
      -- ğŸ‰ no goals
    Â· rw [Zspan.fract, SetLike.mem_coe, sub_eq_add_neg]
      -- âŠ¢ x + -â†‘(Zspan.floor b x) âˆˆ L
      refine AddSubgroup.add_mem _ h_mem
        (neg_mem (Set.mem_of_subset_of_mem ?_ (Subtype.mem (Zspan.floor b x))))
      rw [show (L : Set E) = AddSubgroup.toIntSubmodule L by rfl]
      -- âŠ¢ â†‘(span â„¤ (Set.range â†‘b)) âŠ† â†‘(â†‘AddSubgroup.toIntSubmodule L)
      rw [SetLike.coe_subset_coe, Basis.coe_mk, Subtype.range_coe_subtype, Set.setOf_mem_eq]
      -- âŠ¢ span â„¤ s â‰¤ â†‘AddSubgroup.toIntSubmodule L
      exact span_le.mpr h_incl
      -- ğŸ‰ no goals
  Â· -- `span â„¤ s` is finitely generated because `s` is finite
    rw [ker_mkQ, inf_of_le_right (span_le.mpr h_incl)]
    -- âŠ¢ Submodule.FG (span â„¤ s)
    exact fg_span (LinearIndependent.finite h_lind)
    -- ğŸ‰ no goals

theorem Zlattice.module_finite : Module.Finite â„¤ L :=
  Module.Finite.iff_addGroup_fg.mpr ((AddGroup.fg_iff_addSubgroup_fg L).mpr (FG K hs))

theorem Zlattice.module_free : Module.Free â„¤ L := by
  have : Module.Finite â„¤ L := module_finite K hs
  -- âŠ¢ Module.Free â„¤ { x // x âˆˆ L }
  have : Module â„š E := Module.compHom E (algebraMap â„š K)
  -- âŠ¢ Module.Free â„¤ { x // x âˆˆ L }
  have : NoZeroSMulDivisors â„¤ E := RatModule.noZeroSMulDivisors
  -- âŠ¢ Module.Free â„¤ { x // x âˆˆ L }
  have : NoZeroSMulDivisors â„¤ L := by
    change NoZeroSMulDivisors â„¤ (AddSubgroup.toIntSubmodule L)
    exact noZeroSMulDivisors _
  exact Module.free_of_finite_type_torsion_free'
  -- ğŸ‰ no goals

open FiniteDimensional

theorem Zlattice.rank : finrank â„¤ L = finrank K E := by
  classical
  have : Module.Finite â„¤ L := module_finite K hs
  have : Module.Free â„¤ L := module_free K hs
  have : Module â„š E := Module.compHom E (algebraMap â„š K)
  let bâ‚€ := Module.Free.chooseBasis â„¤ L
  -- Let `b` be a `â„¤`-basis of `L` formed of vectors of `E`
  let b := Subtype.val âˆ˜ bâ‚€
  have : LinearIndependent â„¤ b :=
    LinearIndependent.map' bâ‚€.linearIndependent (L.toIntSubmodule.subtype) (ker_subtype _)
  -- We prove some assertions that will be useful later on
  have h_spanL : span â„¤ (Set.range b) = AddSubgroup.toIntSubmodule L := by
    convert congrArg (map (Submodule.subtype (AddSubgroup.toIntSubmodule L))) bâ‚€.span_eq
    Â· rw [map_span, Set.range_comp]
      rfl
    Â· exact (map_subtype_top _).symm
  have h_spanE : span K (Set.range b) = âŠ¤ := by rwa [â† span_span_of_tower (R := â„¤), h_spanL]
  have h_card : Fintype.card (Module.Free.ChooseBasisIndex â„¤ L) =
      (Set.range b).toFinset.card := by
    rw [Set.toFinset_range, Finset.univ.card_image_of_injective]
    rfl
    exact Subtype.coe_injective.comp (Basis.injective _)
  rw [finrank_eq_card_chooseBasisIndex]
    -- We prove that `finrank â„¤ L â‰¤ finrank K E` and `finrank K E â‰¤ finrank â„¤ L`
  refine le_antisymm ?_ ?_
  Â· -- To prove that `finrank â„¤ L â‰¤ finrank K E`, we proceed by contradiction and prove that, in
    -- this case, there is a â„¤-relation between the vectors of `b`
    obtain âŸ¨t, âŸ¨ht_inc, âŸ¨ht_span, ht_linâŸ©âŸ©âŸ© := exists_linearIndependent K (Set.range b)
    -- `e` is a `K`-basis of `E` formed of vectors of `b`
    let e : Basis t K E := Basis.mk ht_lin (by simp [ht_span, h_spanE])
    have : Fintype t := Set.Finite.fintype ((Set.range b).toFinite.subset ht_inc)
    have h : LinearIndependent â„¤ (fun x : (Set.range b) => (x : E)) := by
      rwa [linearIndependent_subtype_range (Subtype.coe_injective.comp bâ‚€.injective)]
    contrapose! h
    -- Since `finrank â„¤ L > finrank K E`, there exists a vector `v âˆˆ b` with `v âˆ‰ e`
    obtain âŸ¨v, hvâŸ© : (Set.range b \ Set.range e).Nonempty := by
      rw [Basis.coe_mk, Subtype.range_coe_subtype, Set.setOf_mem_eq, â† Set.toFinset_nonempty]
      contrapose h
      rw [Finset.not_nonempty_iff_eq_empty, Set.toFinset_diff,
        Finset.sdiff_eq_empty_iff_subset] at h
      replace h := Finset.card_le_of_subset h
      rwa [not_lt, h_card, â† topEquiv.finrank_eq, â† h_spanE, â† ht_span,
        finrank_span_set_eq_card _ ht_lin]
    -- Assume that `e âˆª {v}` is not `â„¤`-linear independent then we get the contradiction
    suffices Â¬ LinearIndependent â„¤ (fun x : â†¥(insert v (Set.range e)) => (x : E)) by
      contrapose! this
      refine LinearIndependent.mono ?_ this
      exact Set.insert_subset (Set.mem_of_mem_diff hv) (by simp [ht_inc])
    -- We prove finally that `e âˆª {v}` is not â„¤-linear independent or, equivalently,
    -- not â„š-linear independent by showing that `v âˆˆ span â„š e`.
    rw [LinearIndependent.iff_fractionRing â„¤ â„š,
      (linearIndependent_insert (Set.not_mem_of_mem_diff hv)),  not_and, not_not]
    intro _
    -- But that follows from the fact that there exist `n, m : â„•`, `n â‰  m`
    -- such that `(n - m) â€¢ v âˆˆ span â„¤ e` which is true since `n â†¦ Zspan.fract e (n â€¢ v)`
    -- takes value into the finite set `fundamentalDomain e âˆ© L`
    have h_mapsto : Set.MapsTo (fun n : â„¤ => Zspan.fract e (n â€¢ v)) Set.univ
        (Metric.closedBall 0 (âˆ‘ i, â€–e iâ€–) âˆ© (L : Set E)) := by
      rw [Set.mapsTo_inter, Set.maps_univ_to, Set.maps_univ_to]
      refine âŸ¨fun _ =>  mem_closedBall_zero_iff.mpr (Zspan.norm_fract_le e _), fun _ => ?_âŸ©
      Â· change _ âˆˆ AddSubgroup.toIntSubmodule L
        rw [â† h_spanL]
        refine sub_mem ?_ ?_
        Â· exact zsmul_mem (subset_span (Set.diff_subset _ _ hv)) _
        Â· exact span_mono (by simp [ht_inc]) (coe_mem _)
    have h_finite : Set.Finite (Metric.closedBall 0 (âˆ‘ i, â€–e iâ€–) âˆ© (L : Set E)) :=
      Metric.Finite_bounded_inter_isClosed Metric.bounded_closedBall inferInstance
    obtain âŸ¨n, -, m, -, h_neq, h_eqâŸ© := Set.Infinite.exists_ne_map_eq_of_mapsTo
      Set.infinite_univ h_mapsto h_finite
    have h_nz : (-n + m : â„š) â‰  0 := by
      rwa [Ne.def, add_eq_zero_iff_eq_neg.not, neg_inj, Rat.coe_int_inj, â† Ne.def]
    apply (smul_mem_iff _ h_nz).mp
    refine span_subset_span â„¤ â„š _ ?_
    rwa [add_smul, neg_smul, SetLike.mem_coe, â† Zspan.fract_eq_fract, â† zsmul_eq_smul_cast â„š,
      â† zsmul_eq_smul_cast â„š]
  Â· -- To prove that `finrank K E â‰¤ finrank â„¤ L`, we use the fact `b` generates `E` over `K`
    -- and thus `finrank K E â‰¤ card b = finrank â„¤ L`
    rw [â† topEquiv.finrank_eq, â† h_spanE]
    convert finrank_span_le_card (K := K) (Set.range b)

end Zlattice
