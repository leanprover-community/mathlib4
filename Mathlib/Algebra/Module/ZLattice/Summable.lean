/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Algebra.BigOperators.Group.Finset.Disjoint
import Mathlib.Algebra.Module.ZLattice.Basic
import Mathlib.Analysis.PSeries

/-!
# Convergence of `p`-series on lattices

Let `E` be a finite dimensional normed `â„`-space, and `L` a discrete subgroup of `E` of rank `d`.
We show that `âˆ‘ z âˆˆ L, â€–z - xâ€–Ê³` is convergent for `r < -d`.

## Main results
- `ZLattice.summable_norm_rpow`: `âˆ‘ z âˆˆ L, â€–zâ€–Ê³` converges when `r < -d`.
- `ZLattice.summable_norm_sub_rpow`: `âˆ‘ z âˆˆ L, â€–z - xâ€–Ê³` converges when `r < -d`.
- `ZLattice.tsum_norm_rpow_le`:
  `âˆ‘ z âˆˆ L, â€–zâ€–Ê³ â‰¤ AÊ³ * âˆ‘ k : â„•, káµˆâºÊ³â»Â¹` for some `A > 0` depending only on `L`.

-/

noncomputable section

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
variable [FiniteDimensional â„ E] {L : Submodule â„¤ E} [DiscreteTopology L]
variable {Î¹ : Type*} (b : Basis Î¹ â„¤ L)

lemma ZLattice.exists_forall_abs_repr_le_norm :
    âˆƒ (Îµ : â„), 0 < Îµ âˆ§ âˆ€ (x : L), âˆ€ i, Îµ * |b.repr x i| â‰¤ â€–xâ€– := by
  wlog H : IsZLattice â„ L
  Â· have inst : Finite Î¹ := Module.Finite.finite_basis b
    let E' := Submodule.span â„ (L : Set E)
    let L' : Submodule â„¤ E' := ZLattice.comap â„ L E'.subtype
    have inst : DiscreteTopology L' :=
      ZLattice.comap_discreteTopology _ _ (by fun_prop) Subtype.val_injective
    let e : L' â‰ƒâ‚—[â„¤] L := Submodule.comapSubtypeEquivOfLe (p := L) (q := E'.restrictScalars â„¤)
      Submodule.subset_span
    have inst : IsZLattice â„ L' := by
      refine âŸ¨Submodule.map_injective_of_injective (f := E'.subtype) Subtype.val_injective ?_âŸ©
      simp [Submodule.map_span, E', L', Set.image_preimage_eq_inter_range,
        Set.inter_eq_left.mpr Submodule.subset_span]
    obtain âŸ¨Îµ, hÎµ, HâŸ© := this (b.map e.symm) inst
    exact âŸ¨Îµ, hÎµ, fun x i â†¦ by simpa using H âŸ¨âŸ¨x.1, Submodule.subset_span x.2âŸ©, x.2âŸ© iâŸ©
  have : Finite Î¹ := Module.Finite.finite_basis b
  let b' : Basis Î¹ â„ E := Basis.ofZLatticeBasis â„ L b
  let e := ((b'.repr â‰ªâ‰«â‚— Finsupp.linearEquivFunOnFinite _ _ _).toContinuousLinearEquiv (ğ•œ := â„))
  have := e.continuous.1 (Set.univ.pi fun _ â†¦ Set.Ioo (-1) 1)
    (isOpen_set_pi Set.finite_univ fun _ _ â†¦ isOpen_Ioo)
  obtain âŸ¨Îµ, hÎµ, hÎµ'âŸ© := Metric.isOpen_iff.mp this 0 (by simp)
  refine âŸ¨Îµ / 2, by positivity, fun x i â†¦ ?_âŸ©
  by_cases hx : x = 0
  Â· simp [hx]
  have hx : â€–x.1â€– â‰  0 := by simpa
  have : |Îµ / 2 * (â€–â†‘xâ€–â»Â¹ * (b.repr x) i)| < 1 := by
    simpa [e, b', â† abs_lt] using @hÎµ' ((Îµ / 2) â€¢ â€–xâ€–â»Â¹ â€¢ x)
      (by simpa [norm_smul, inv_mul_cancelâ‚€ hx, abs_eq_self.mpr hÎµ.le]) i trivial
  rw [abs_mul, abs_mul, abs_inv, mul_left_comm, abs_norm, inv_mul_lt_iffâ‚€ (by positivity),
    mul_one, abs_eq_self.mpr (by positivity), â† Int.cast_abs] at this
  exact this.le


/--
Given a basis of a (possibly not full rank) `â„¤`-lattice, there exists a `Îµ > 0` such that
`|b.repr x i| < n` for all `â€–xâ€– < n * Îµ` (i.e. `b.repr x i = O(x)` depending only on `b`).
This is an arbitrary choice of such an `Îµ`.
-/
def ZLattice.normBound {Î¹ : Type*} (b : Basis Î¹ â„¤ L) : â„ :=
  (exists_forall_abs_repr_le_norm b).choose

lemma ZLattice.normBound_pos {Î¹ : Type*} (b : Basis Î¹ â„¤ L) : 0 < normBound b :=
  (exists_forall_abs_repr_le_norm b).choose_spec.1

lemma ZLattice.normBound_spec {Î¹ : Type*} (b : Basis Î¹ â„¤ L) (x : L) (i : Î¹) :
    normBound b * |b.repr x i| â‰¤ â€–xâ€– :=
  (exists_forall_abs_repr_le_norm b).choose_spec.2 x i

lemma ZLattice.abs_repr_le {Î¹ : Type*} (b : Basis Î¹ â„¤ L) (x : L) (i : Î¹) :
    |b.repr x i| â‰¤ (normBound b)â»Â¹ * â€–xâ€– := by
  rw [le_inv_mul_iffâ‚€ (normBound_pos b)]
  exact normBound_spec b x i

lemma ZLattice.abs_repr_lt_of_norm_lt {Î¹ : Type*} (b : Basis Î¹ â„¤ L) (x : L) (n : â„•)
    (hxn : â€–xâ€– < normBound b * n) (i : Î¹) : |b.repr x i| < n := by
  refine Int.cast_lt.mp ((abs_repr_le b x i).trans_lt ?_)
  rwa [inv_mul_lt_iffâ‚€ (normBound_pos b)]

lemma ZLattice.le_norm_of_le_abs_repr {Î¹ : Type*} (b : Basis Î¹ â„¤ L) (x : L) (n : â„•) (i : Î¹)
    (hi : n â‰¤ |b.repr x i|) : normBound b * n â‰¤ â€–xâ€– := by
  contrapose! hi
  exact abs_repr_lt_of_norm_lt b x n hi i

open Finset in
lemma ZLattice.sum_piFinset_Icc_rpow_le {Î¹ : Type*} [Fintype Î¹] [DecidableEq Î¹]
    (b : Basis Î¹ â„¤ L) {d : â„•} (hd : d = Fintype.card Î¹)
    (n : â„•) (r : â„) (hr : r < -d) :
    âˆ‘ p âˆˆ Fintype.piFinset fun _ : Î¹ â†¦ Icc (-n : â„¤) n, â€–âˆ‘ i, p i â€¢ b iâ€– ^ r â‰¤
      2 * d * 3 ^ (d - 1) * normBound b ^ r * âˆ‘' k : â„•, (k : â„) ^ (d - 1 + r) := by
  let s (n : â„•) := Fintype.piFinset fun i : Î¹ â†¦ Icc (-n : â„¤) n
  subst hd
  set d := Fintype.card Î¹
  have hr' : r < 0 := hr.trans_le (by linarith)
  by_cases hd : d = 0
  Â· have : IsEmpty Î¹ := Fintype.card_eq_zero_iff.mp hd
    simp [hd, hr'.ne]
  replace hd : 1 â‰¤ d := by rwa [Nat.one_le_iff_ne_zero]
  have hs0 : s 0 = {0} := by ext; simp [s, funext_iff]
  have hs {a b : â„•} (ha : a â‰¤ b) : s a âŠ† s b := by
    intros x hx
    simp only [Fintype.mem_piFinset, s] at hx âŠ¢
    exact fun i â†¦ Icc_subset_Icc (by simpa) (by simpa) (hx i)
  have (k : â„•) : #(s (k + 1) \ s k) â‰¤ 2 * d * (2 * k + 3) ^ (d - 1) := by
    trans (2 * k + 3) ^ d - (2 * k + 1) ^ d
    Â· rw [card_sdiff]
      Â· simp only [Fintype.card_piFinset, Int.card_Icc, prod_const, card_univ, s, sub_neg_eq_add]
        gcongr <;> norm_cast <;> omega
      Â· exact hs (by simp)
    Â· have := abs_pow_sub_pow_le (Î± := â„¤) â†‘(2 * k + 3) â†‘(2 * k + 1) d
      simp only [Nat.cast_add, Nat.cast_mul, Nat.cast_ofNat, Nat.cast_one,
        add_sub_add_left_eq_sub, Int.reduceSub, Nat.abs_ofNat, s] at this
      zify
      convert this using 3
      Â· rw [abs_eq_self.mpr, Nat.cast_sub]
        Â· simp
        Â· gcongr; omega
        Â· rw [sub_nonneg]
          gcongr; omega
      Â· rw [max_eq_left, abs_eq_self.mpr]
        Â· positivity
        gcongr; omega
  let Îµ := normBound b
  have hÎµ : 0 < Îµ := normBound_pos b
  calc âˆ‘ p âˆˆ s n, â€–âˆ‘ i, p i â€¢ b iâ€– ^ r
      = âˆ‘ k âˆˆ range n, âˆ‘ p âˆˆ (s (k + 1) \ s k), â€–âˆ‘ i, p i â€¢ b iâ€– ^ r := by
        rw [Finset.sum_eq_sum_range_sdiff _ @hs]
        simp [hs0, hr'.ne]
    _ â‰¤ âˆ‘ k âˆˆ range n, âˆ‘ p âˆˆ (s (k + 1) \ s k), ((k + 1) * Îµ) ^ r := by
        gcongr âˆ‘ k âˆˆ Finset.range n, âˆ‘ p âˆˆ (s (k + 1) \ s k), ?_ with k hk v hv
        rw [â† Nat.cast_one, â† Nat.cast_add]
        refine Real.rpow_le_rpow_of_nonpos (by positivity) ?_ hr'.le
        obtain âŸ¨j, hjâŸ© : âˆƒ i, v i âˆ‰ Icc (-k : â„¤) k := by simpa [s] using (mem_sdiff.mp hv).2
        rw [mul_comm]
        refine ZLattice.le_norm_of_le_abs_repr b _ _ j ?_
        suffices â†‘k + 1 â‰¤ |v j| by simpa [Finsupp.single_apply] using this
        by_contra! H
        rw [Int.lt_add_one_iff, abs_le, â† Finset.mem_Icc] at H
        exact hj H
    _ = âˆ‘ k âˆˆ range n, #(s (k + 1) \ s k) * ((k + 1) * Îµ) ^ r := by
      simp only [sum_const, nsmul_eq_mul]
    _ â‰¤ âˆ‘ k âˆˆ range n, â†‘(2 * d * (2 * k + 3) ^ (d - 1)) * ((k + 1) * Îµ) ^ r := by
      gcongr with k hk
      Â· exact this _
    _ â‰¤ âˆ‘ k âˆˆ range n, â†‘(2 * d * (3 * (k + 1)) ^ (d - 1)) * ((k + 1) * Îµ) ^ r := by
      gcongr; omega
    _ = 2 * d * 3 ^ (d - 1) * Îµ ^ r * âˆ‘ k âˆˆ range n, (k + 1) ^ (d - 1) * (k + 1 : â„) ^ r := by
      simp_rw [Finset.mul_sum]
      congr with k
      push_cast
      rw [Real.mul_rpow, mul_pow]
      Â· group
      Â· positivity
      Â· exact (ZLattice.normBound_pos b).le
    _ = 2 * d * 3 ^ (d - 1) * Îµ ^ r * âˆ‘ k âˆˆ range n, (â†‘(k + 1) : â„) ^ (d - 1 + r) := by
      congr with k
      rw [â† Real.rpow_natCast, â† Real.rpow_add (by positivity), Nat.cast_sub hd]
      norm_cast
    _ â‰¤ 2 * d * 3 ^ (d - 1) * Îµ ^ r * âˆ‘ k âˆˆ range (n + 1), (k : â„) ^ (d - 1 + r) := by
      gcongr
      rw [Finset.sum_range_succ', le_add_iff_nonneg_right]
      positivity
    _ â‰¤ 2 * d * 3 ^ (d - 1) * Îµ ^ r * âˆ‘' k : â„•, (k : â„) ^ (d - 1 + r) := by
      gcongr
      refine sum_le_tsum _ (fun _ _ â†¦ by positivity) ?_
      rw [Real.summable_nat_rpow]
      linarith

variable (L)

lemma ZLattice.exists_finset_sum_norm_rpow_le_tsum :
    âˆƒ A > (0 : â„), âˆ€ r < (-Module.finrank â„¤ L : â„), âˆ€ s : Finset L,
      âˆ‘ z âˆˆ s, â€–zâ€– ^ r â‰¤ A ^ r * âˆ‘' k : â„•, (k : â„) ^ (Module.finrank â„¤ L - 1 + r) := by
  cases subsingleton_or_nontrivial L
  Â· refine âŸ¨1, zero_lt_one, fun r hr s â†¦ ?_âŸ©
    have hr : r â‰  0 := by linarith
    simpa [Subsingleton.elim _ (0 : L), Real.zero_rpow hr] using tsum_nonneg fun _ â†¦ by positivity
  classical
  let I : Type _ := Module.Free.ChooseBasisIndex â„¤ L
  have : Fintype I := inferInstance
  let b : Basis I â„¤ L := Module.Free.chooseBasis â„¤ L
  simp_rw [Module.finrank_eq_card_basis b]
  set d := Fintype.card I
  have hd : d â‰  0 := by simp [d]
  let Îµ := normBound b
  obtain âŸ¨A, hA, B, hB, HâŸ© : âˆƒ A > (0 : â„), âˆƒ B > (0 : â„), âˆ€ r < (-d : â„), âˆ€ s : Finset L,
      âˆ‘ z âˆˆ s, â€–zâ€– ^ r â‰¤ A * B ^ r * âˆ‘' k : â„•, (k : â„) ^ (d - 1 + r) := by
    refine âŸ¨2 * d * 3 ^ (d - 1), by positivity, Îµ, normBound_pos b, fun r hr u â†¦ ?_âŸ©
    let e : (I â†’ â„¤) â‰ƒâ‚—[â„¤] L := (b.repr â‰ªâ‰«â‚— Finsupp.linearEquivFunOnFinite _ _ _).symm
    obtain âŸ¨u, rflâŸ© : âˆƒ u' : Finset _, u = u'.image e.toEmbedding :=
      âŸ¨u.image e.symm.toEmbedding, Finset.coe_injective
        (by simpa using (e.image_symm_image _).symm)âŸ©
    dsimp
    simp only [LinearEquiv.coe_addEquiv_apply, EmbeddingLike.apply_eq_iff_eq,
      imp_self, implies_true, Finset.sum_image, ge_iff_le]
    obtain âŸ¨n, hnâŸ© : âˆƒ n : â„•, u âŠ† Fintype.piFinset fun _ : I â†¦ Finset.Icc (-n : â„¤) n := by
      obtain âŸ¨r, hr, hr'âŸ© := u.finite_toSet.isCompact.isBounded.subset_closedBall_lt 0 0
      refine âŸ¨âŒŠrâŒ‹.toNat, fun x hx â†¦ ?_âŸ©
      have hr'' : âŒŠrâŒ‹ âŠ” 0 = âŒŠrâŒ‹ := by simp; positivity
      have := hr' hx
      simp only [Metric.mem_closedBall, dist_zero_right, pi_norm_le_iff_of_nonneg hr.le,
        Int.norm_eq_abs, â† Int.cast_abs, â† Int.le_floor] at this
      simpa only [Int.ofNat_toNat, Fintype.mem_piFinset, Finset.mem_Icc, â† abs_le, hr'']
    refine (Finset.sum_le_sum_of_subset_of_nonneg hn (by intros; positivity)).trans ?_
    dsimp
    simp only [Submodule.norm_coe]
    convert ZLattice.sum_piFinset_Icc_rpow_le b rfl n r hr with x
    simp [e, Finsupp.linearCombination]
    rfl
  have (r : â„) : 0 â‰¤ âˆ‘' k : â„•, (k : â„) ^ (d - 1 + r) := tsum_nonneg fun _ â†¦ by positivity
  by_cases hA' : A â‰¤ 1
  Â· refine âŸ¨B, hB, fun r hr s â†¦ (H r hr s).trans ?_âŸ©
    rw [mul_assoc]
    exact mul_le_of_le_one_left (mul_nonneg (by positivity) (this r)) hA'
  Â· refine âŸ¨Aâ»Â¹ * B, mul_pos (inv_pos.mpr hA) hB, fun r hr s â†¦ (H r hr s).trans ?_âŸ©
    rw [Real.mul_rpow (inv_pos.mpr hA).le hB.le, mul_assoc, mul_assoc]
    refine mul_le_mul_of_nonneg_right ?_ (mul_nonneg (by positivity) (this r))
    rw [â† Real.rpow_neg_one, â† Real.rpow_mul hA.le]
    refine Real.self_le_rpow_of_one_le (not_le.mp hA').le ?_
    simp only [neg_mul, one_mul, le_neg (b := r)]
    refine hr.le.trans ?_
    norm_num
    exact Nat.one_le_iff_ne_zero.mpr hd

/--
Let `L` be a lattice with (possibly non-full) rank `d`, and `r : â„` such that `d < r`.
Then `âˆ‘ z âˆˆ L, â€–zâ€–â»Ê³ â‰¤ Aâ»Ê³ * âˆ‘ k : â„•, káµˆâ»Ê³â»Â¹` for some `A > 0` depending only on `L`.
This is an arbitrary choice of `A`. See `ZLattice.tsum_norm_rpow_le`.
-/
def ZLattice.tsumNormRPowBound : â„ :=
  (exists_finset_sum_norm_rpow_le_tsum L).choose

lemma ZLattice.tsumNormRPowBound_pos : 0 < tsumNormRPowBound L :=
  (exists_finset_sum_norm_rpow_le_tsum L).choose_spec.1

lemma ZLattice.tsumNormRPowBound_spec (r : â„) (h : r < -Module.finrank â„¤ L) (s : Finset L) :
    âˆ‘ z âˆˆ s, â€–zâ€– ^ r â‰¤
      tsumNormRPowBound L ^ r * âˆ‘' k : â„•, (k : â„) ^ (Module.finrank â„¤ L - 1 + r) :=
  (ZLattice.exists_finset_sum_norm_rpow_le_tsum L).choose_spec.2 r h s

/-- If `L` is a `â„¤`-lattice with rank `d` in `E`, then `âˆ‘ z âˆˆ L, â€–zâ€–Ê³` converges when `r < -d`. -/
lemma ZLattice.summable_norm_rpow (r : â„) (hr : r < -Module.finrank â„¤ L) :
    Summable fun z : L â†¦ â€–zâ€– ^ r :=
  summable_of_sum_le (fun _ â†¦ by positivity) (tsumNormRPowBound_spec L r hr)

/-- `âˆ‘ z âˆˆ L, â€–zâ€–â»Ê³ â‰¤ Aâ»Ê³ * âˆ‘ k : â„•, káµˆâ»Ê³â»Â¹` for some `A > 0` depending only on `L`. -/
lemma ZLattice.tsum_norm_rpow_le (r : â„) (hr : r < -Module.finrank â„¤ L) :
    âˆ‘' z : L, â€–zâ€– ^ r â‰¤
      tsumNormRPowBound L ^ r * âˆ‘' k : â„•, (k : â„) ^ (Module.finrank â„¤ L - 1 + r) :=
  tsum_le_of_sum_le (summable_norm_rpow L r hr) (tsumNormRPowBound_spec L r hr)

lemma ZLattice.summable_norm_sub_rpow (r : â„) (hr : r < -Module.finrank â„¤ L) (x : E) :
    Summable fun z : L â†¦ â€–z - xâ€– ^ r := by
  cases subsingleton_or_nontrivial L
  Â· exact .of_finite
  refine Summable.of_norm_bounded_eventually _
    (.mul_left ((1 / 2) ^ r) (summable_norm_rpow L r hr)) ?_
  have H : IsClosed (X := E) L := @AddSubgroup.isClosed_of_discrete _ _ _ _ _
    L.toAddSubgroup (inferInstanceAs (DiscreteTopology L))
  refine ((Metric.finite_isBounded_inter_isClosed
    (Metric.isBounded_closedBall (x := (0 : E)) (r := 2 * â€–xâ€–)) H).preimage_embedding
    (.subtype _)).subset ?_
  intro t ht
  by_cases htâ‚ : â€–tâ€– = 0
  Â· simp [show t = 0 by simpa using htâ‚]
  by_cases htâ‚‚ : â€–t - xâ€– = 0
  Â· simpa [show t = x by simpa [sub_eq_zero] using htâ‚‚, two_mul] using t.2
  have : 0 < Module.finrank â„¤ L := Module.finrank_pos
  have : â€–t - xâ€– < 2â»Â¹ * â€–tâ€– := by
    rw [â† Real.rpow_lt_rpow_iff_of_neg (by positivity) (by positivity) (hr.trans (by simpa))]
    simpa [Real.mul_rpow, abs_eq_self.mpr (show 0 â‰¤ â€–t - xâ€– ^ r by positivity)] using ht
  have := (norm_sub_norm_le _ _).trans_lt this
  rw [sub_lt_iff_lt_add, â† sub_lt_iff_lt_add', AddSubgroupClass.coe_norm] at this
  simpa using show â€–t.1â€– â‰¤ 2 * â€–xâ€– by linarith

lemma ZLattice.summable_norm_sub_zpow (n : â„¤) (hn : n < -Module.finrank â„¤ L) (x : E) :
    Summable fun z : L â†¦ â€–z - xâ€– ^ n := by
  exact_mod_cast summable_norm_sub_rpow L n (by exact_mod_cast hn) x

lemma ZLattice.summable_norm_zpow (n : â„¤) (hn : n < -Module.finrank â„¤ L) :
    Summable fun z : L â†¦ â€–zâ€– ^ n := by
  simpa using summable_norm_sub_zpow L n hn 0

lemma ZLattice.summable_norm_sub_inv_pow (n : â„•) (hn : Module.finrank â„¤ L < n) (x : E) :
    Summable fun z : L â†¦ â€–z - xâ€–â»Â¹ ^ n := by
  simpa using summable_norm_sub_zpow L (-n) (by gcongr) x

lemma ZLattice.summable_norm_pow_inv (n : â„•) (hn : Module.finrank â„¤ L < n) :
    Summable fun z : L â†¦ â€–zâ€–â»Â¹ ^ n := by
  simpa using summable_norm_sub_inv_pow L n hn 0
