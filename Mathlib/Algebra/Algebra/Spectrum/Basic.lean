/-
Copyright (c) 2021 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Algebra.Algebra.Subalgebra.Basic
import Mathlib.Algebra.Star.Pointwise
import Mathlib.RingTheory.Ideal.Maps
import Mathlib.RingTheory.Ideal.Nonunits
import Mathlib.Tactic.NoncommRing

/-!
# Spectrum of an element in an algebra
This file develops the basic theory of the spectrum of an element of an algebra.
This theory will serve as the foundation for spectral theory in Banach algebras.

## Main definitions

* `resolventSet a : Set R`: the resolvent set of an element `a : A` where
  `A` is an `R`-algebra.
* `spectrum a : Set R`: the spectrum of an element `a : A` where
  `A` is an `R`-algebra.
* `resolvent : R â†’ A`: the resolvent function is `fun r â†¦ Ring.inverse (â†‘â‚ r - a)`, and hence
  when `r âˆˆ resolvent R A`, it is actually the inverse of the unit `(â†‘â‚ r - a)`.

## Main statements

* `spectrum.unit_smul_eq_smul` and `spectrum.smul_eq_smul`: units in the scalar ring commute
  (multiplication) with the spectrum, and over a field even `0` commutes with the spectrum.
* `spectrum.left_add_coset_eq`: elements of the scalar ring commute (addition) with the spectrum.
* `spectrum.unit_mem_mul_comm` and `spectrum.preimage_units_mul_comm`: the
  units (of `R`) in `Ïƒ (a*b)` coincide with those in `Ïƒ (b*a)`.
* `spectrum.scalar_eq`: in a nontrivial algebra over a field, the spectrum of a scalar is
  a singleton.

## Notation

* `Ïƒ a` : `spectrum R a` of `a : A`
-/


open Set

open scoped Pointwise

universe u v

section Defs

variable (R : Type u) {A : Type v}
variable [CommSemiring R] [Ring A] [Algebra R A]

local notation "â†‘â‚" => algebraMap R A

-- definition and basic properties
/-- Given a commutative ring `R` and an `R`-algebra `A`, the *resolvent set* of `a : A`
is the `Set R` consisting of those `r : R` for which `râ€¢1 - a` is a unit of the
algebra `A`. -/
def resolventSet (a : A) : Set R :=
  {r : R | IsUnit (â†‘â‚ r - a)}

/-- Given a commutative ring `R` and an `R`-algebra `A`, the *spectrum* of `a : A`
is the `Set R` consisting of those `r : R` for which `râ€¢1 - a` is not a unit of the
algebra `A`.

The spectrum is simply the complement of the resolvent set. -/
def spectrum (a : A) : Set R :=
  (resolventSet R a)á¶œ

variable {R}

/-- Given an `a : A` where `A` is an `R`-algebra, the *resolvent* is
    a map `R â†’ A` which sends `r : R` to `(algebraMap R A r - a)â»Â¹` when
    `r âˆˆ resolvent R A` and `0` when `r âˆˆ spectrum R A`. -/
noncomputable def resolvent (a : A) (r : R) : A :=
  Ring.inverse (â†‘â‚ r - a)

/-- The unit `1 - râ»Â¹ â€¢ a` constructed from `r â€¢ 1 - a` when the latter is a unit. -/
@[simps]
noncomputable def IsUnit.subInvSMul {r : RË£} {s : R} {a : A} (h : IsUnit <| r â€¢ â†‘â‚ s - a) : AË£ where
  val := â†‘â‚ s - râ»Â¹ â€¢ a
  inv := r â€¢ â†‘h.unitâ»Â¹
  val_inv := by rw [mul_smul_comm, â† smul_mul_assoc, smul_sub, smul_inv_smul, h.mul_val_inv]
  inv_val := by rw [smul_mul_assoc, â† mul_smul_comm, smul_sub, smul_inv_smul, h.val_inv_mul]

end Defs

namespace spectrum

section ScalarSemiring

variable {R : Type u} {A : Type v}
variable [CommSemiring R] [Ring A] [Algebra R A]

local notation "Ïƒ" => spectrum R

local notation "â†‘â‚" => algebraMap R A

theorem mem_iff {r : R} {a : A} : r âˆˆ Ïƒ a â†” Â¬IsUnit (â†‘â‚ r - a) :=
  Iff.rfl

theorem notMem_iff {r : R} {a : A} : r âˆ‰ Ïƒ a â†” IsUnit (â†‘â‚ r - a) := by
  simp [mem_iff]

@[deprecated (since := "2025-05-23")] alias not_mem_iff := notMem_iff

variable (R)

theorem zero_mem_iff {a : A} : (0 : R) âˆˆ Ïƒ a â†” Â¬IsUnit a := by
  rw [mem_iff, map_zero, zero_sub, IsUnit.neg_iff]

alias âŸ¨not_isUnit_of_zero_mem, zero_memâŸ© := spectrum.zero_mem_iff

theorem zero_notMem_iff {a : A} : (0 : R) âˆ‰ Ïƒ a â†” IsUnit a := by
  rw [zero_mem_iff, Classical.not_not]

@[deprecated (since := "2025-05-23")] alias zero_not_mem_iff := zero_notMem_iff

alias âŸ¨isUnit_of_zero_notMem, zero_notMemâŸ© := spectrum.zero_not_mem_iff

@[deprecated (since := "2025-05-23")] alias isUnit_of_zero_not_mem := isUnit_of_zero_notMem
@[deprecated (since := "2025-05-23")] alias zero_not_mem := zero_notMem

@[simp]
lemma _root_.Units.zero_notMem_spectrum (a : AË£) : 0 âˆ‰ spectrum R (a : A) :=
  spectrum.zero_notMem R a.isUnit

@[deprecated (since := "2025-05-23")]
alias _root_.Units.zero_not_mem_spectrum := _root_.Units.zero_notMem_spectrum

lemma subset_singleton_zero_compl {a : A} (ha : IsUnit a) : spectrum R a âŠ† {0}á¶œ :=
  Set.subset_compl_singleton_iff.mpr <| spectrum.zero_notMem R ha

variable {R}

theorem mem_resolventSet_of_left_right_inverse {r : R} {a b c : A} (hâ‚ : (â†‘â‚ r - a) * b = 1)
    (hâ‚‚ : c * (â†‘â‚ r - a) = 1) : r âˆˆ resolventSet R a :=
  Units.isUnit âŸ¨â†‘â‚ r - a, b, hâ‚, by rwa [â† left_inv_eq_right_inv hâ‚‚ hâ‚]âŸ©

theorem mem_resolventSet_iff {r : R} {a : A} : r âˆˆ resolventSet R a â†” IsUnit (â†‘â‚ r - a) :=
  Iff.rfl

@[simp]
theorem algebraMap_mem_iff (S : Type*) {R A : Type*} [CommSemiring R] [CommSemiring S]
    [Ring A] [Algebra R S] [Algebra R A] [Algebra S A] [IsScalarTower R S A] {a : A} {r : R} :
    algebraMap R S r âˆˆ spectrum S a â†” r âˆˆ spectrum R a := by
  simp only [spectrum.mem_iff, Algebra.algebraMap_eq_smul_one, smul_assoc, one_smul]

protected alias âŸ¨of_algebraMap_mem, algebraMap_memâŸ© := spectrum.algebraMap_mem_iff

@[simp]
theorem preimage_algebraMap (S : Type*) {R A : Type*} [CommSemiring R] [CommSemiring S]
    [Ring A] [Algebra R S] [Algebra R A] [Algebra S A] [IsScalarTower R S A] {a : A} :
    algebraMap R S â»Â¹' spectrum S a = spectrum R a :=
  Set.ext fun _ => spectrum.algebraMap_mem_iff _

@[simp]
theorem resolventSet_of_subsingleton [Subsingleton A] (a : A) : resolventSet R a = Set.univ := by
  simp_rw [resolventSet, Subsingleton.elim (algebraMap R A _ - a) 1, isUnit_one, Set.setOf_true]

@[simp]
theorem of_subsingleton [Subsingleton A] (a : A) : spectrum R a = âˆ… := by
  rw [spectrum, resolventSet_of_subsingleton, Set.compl_univ]

theorem resolvent_eq {a : A} {r : R} (h : r âˆˆ resolventSet R a) : resolvent a r = â†‘h.unitâ»Â¹ :=
  Ring.inverse_unit h.unit

theorem units_smul_resolvent {r : RË£} {s : R} {a : A} :
    r â€¢ resolvent a (s : R) = resolvent (râ»Â¹ â€¢ a) (râ»Â¹ â€¢ s : R) := by
  by_cases h : s âˆˆ spectrum R a
  Â· rw [mem_iff] at h
    simp only [resolvent, Algebra.algebraMap_eq_smul_one] at *
    rw [smul_assoc, â† smul_sub]
    have h' : Â¬IsUnit (râ»Â¹ â€¢ (s â€¢ (1 : A) - a)) := fun hu =>
      h (by simpa only [smul_inv_smul] using IsUnit.smul r hu)
    simp only [Ring.inverse_non_unit _ h, Ring.inverse_non_unit _ h', smul_zero]
  Â· simp only [resolvent]
    have h' : IsUnit (r â€¢ algebraMap R A (râ»Â¹ â€¢ s) - a) := by
      simpa [Algebra.algebraMap_eq_smul_one, smul_assoc] using notMem_iff.mp h
    rw [â† h'.val_subInvSMul, â† (notMem_iff.mp h).unit_spec, Ring.inverse_unit, Ring.inverse_unit,
      h'.val_inv_subInvSMul]
    simp only [Algebra.algebraMap_eq_smul_one, smul_assoc, smul_inv_smul]

theorem units_smul_resolvent_self {r : RË£} {a : A} :
    r â€¢ resolvent a (r : R) = resolvent (râ»Â¹ â€¢ a) (1 : R) := by
  simpa only [Units.smul_def, Algebra.id.smul_eq_mul, Units.inv_mul] using
    @units_smul_resolvent _ _ _ _ _ r r a

/-- The resolvent is a unit when the argument is in the resolvent set. -/
theorem isUnit_resolvent {r : R} {a : A} : r âˆˆ resolventSet R a â†” IsUnit (resolvent a r) :=
  isUnit_ringInverse.symm

theorem inv_mem_resolventSet {r : RË£} {a : AË£} (h : (r : R) âˆˆ resolventSet R (a : A)) :
    (â†‘râ»Â¹ : R) âˆˆ resolventSet R (â†‘aâ»Â¹ : A) := by
  rw [mem_resolventSet_iff, Algebra.algebraMap_eq_smul_one, â† Units.smul_def] at h âŠ¢
  rw [IsUnit.smul_sub_iff_sub_inv_smul, inv_inv, IsUnit.sub_iff]
  have hâ‚ : (a : A) * (r â€¢ (â†‘aâ»Â¹ : A) - 1) = r â€¢ (1 : A) - a := by
    rw [mul_sub, mul_smul_comm, a.mul_inv, mul_one]
  have hâ‚‚ : (r â€¢ (â†‘aâ»Â¹ : A) - 1) * a = r â€¢ (1 : A) - a := by
    rw [sub_mul, smul_mul_assoc, a.inv_mul, one_mul]
  have hcomm : Commute (a : A) (r â€¢ (â†‘aâ»Â¹ : A) - 1) := by rwa [â† hâ‚‚] at hâ‚
  exact (hcomm.isUnit_mul_iff.mp (hâ‚.symm â–¸ h)).2

theorem inv_mem_iff {r : RË£} {a : AË£} : (r : R) âˆˆ Ïƒ (a : A) â†” (â†‘râ»Â¹ : R) âˆˆ Ïƒ (â†‘aâ»Â¹ : A) :=
  not_iff_not.2 <| âŸ¨inv_mem_resolventSet, inv_mem_resolventSetâŸ©

theorem zero_mem_resolventSet_of_unit (a : AË£) : 0 âˆˆ resolventSet R (a : A) := by
  simpa only [mem_resolventSet_iff, â† notMem_iff, zero_notMem_iff] using a.isUnit

theorem ne_zero_of_mem_of_unit {a : AË£} {r : R} (hr : r âˆˆ Ïƒ (a : A)) : r â‰  0 := fun hn =>
  (hn â–¸ hr) (zero_mem_resolventSet_of_unit a)

theorem add_mem_iff {a : A} {r s : R} : r + s âˆˆ Ïƒ a â†” r âˆˆ Ïƒ (-â†‘â‚ s + a) := by
  simp only [mem_iff, sub_neg_eq_add, â† sub_sub, map_add]

theorem add_mem_add_iff {a : A} {r s : R} : r + s âˆˆ Ïƒ (â†‘â‚ s + a) â†” r âˆˆ Ïƒ a := by
  rw [add_mem_iff, neg_add_cancel_left]

theorem smul_mem_smul_iff {a : A} {s : R} {r : RË£} : r â€¢ s âˆˆ Ïƒ (r â€¢ a) â†” s âˆˆ Ïƒ a := by
  simp only [mem_iff, Algebra.algebraMap_eq_smul_one, smul_assoc, â† smul_sub, isUnit_smul_iff]

theorem unit_smul_eq_smul (a : A) (r : RË£) : Ïƒ (r â€¢ a) = r â€¢ Ïƒ a := by
  ext x
  have x_eq : x = r â€¢ râ»Â¹ â€¢ x := by simp
  nth_rw 1 [x_eq]
  rw [smul_mem_smul_iff]
  constructor
  Â· exact fun h => âŸ¨râ»Â¹ â€¢ x, âŸ¨h, show r â€¢ râ»Â¹ â€¢ x = x by simpâŸ©âŸ©
  Â· rintro âŸ¨w, _, (x'_eq : r â€¢ w = x)âŸ©
    simpa [â† x'_eq ]

-- `r âˆˆ Ïƒ(a*b) â†” r âˆˆ Ïƒ(b*a)` for any `r : RË£`
theorem unit_mem_mul_comm {a b : A} {r : RË£} : â†‘r âˆˆ Ïƒ (a * b) â†” â†‘r âˆˆ Ïƒ (b * a) := by
  have hâ‚ : âˆ€ x y : A, IsUnit (1 - x * y) â†’ IsUnit (1 - y * x) := by
    refine fun x y h => âŸ¨âŸ¨1 - y * x, 1 + y * h.unit.inv * x, ?_, ?_âŸ©, rflâŸ©
    Â· calc
        (1 - y * x) * (1 + y * (IsUnit.unit h).inv * x) =
            1 - y * x + y * ((1 - x * y) * h.unit.inv) * x := by noncomm_ring
        _ = 1 := by simp only [Units.inv_eq_val_inv, IsUnit.mul_val_inv, mul_one, sub_add_cancel]
    Â· calc
        (1 + y * (IsUnit.unit h).inv * x) * (1 - y * x) =
            1 - y * x + y * (h.unit.inv * (1 - x * y)) * x := by noncomm_ring
        _ = 1 := by simp only [Units.inv_eq_val_inv, IsUnit.val_inv_mul, mul_one, sub_add_cancel]
  have := Iff.intro (hâ‚ (râ»Â¹ â€¢ a) b) (hâ‚ b (râ»Â¹ â€¢ a))
  rw [mul_smul_comm râ»Â¹ b a] at this
  simpa only [mem_iff, not_iff_not, Algebra.algebraMap_eq_smul_one, â† Units.smul_def,
    IsUnit.smul_sub_iff_sub_inv_smul, smul_mul_assoc]

theorem preimage_units_mul_comm (a b : A) :
    ((â†‘) : RË£ â†’ R) â»Â¹' Ïƒ (a * b) = (â†‘) â»Â¹' Ïƒ (b * a) :=
  Set.ext fun _ => unit_mem_mul_comm

theorem setOf_isUnit_inter_mul_comm (a b : A) :
    {r | IsUnit r} âˆ© Ïƒ (a * b) = {r | IsUnit r} âˆ© Ïƒ (b * a) := by
  ext r
  simpa using fun hr : IsUnit r â†¦ unit_mem_mul_comm (r := hr.unit)

section Star

variable [InvolutiveStar R] [StarRing A] [StarModule R A]

theorem star_mem_resolventSet_iff {r : R} {a : A} :
    star r âˆˆ resolventSet R a â†” r âˆˆ resolventSet R (star a) := by
  refine âŸ¨fun h => ?_, fun h => ?_âŸ© <;>
    simpa only [mem_resolventSet_iff, Algebra.algebraMap_eq_smul_one, star_sub, star_smul,
      star_star, star_one] using IsUnit.star h

protected theorem map_star (a : A) : Ïƒ (star a) = star (Ïƒ a) := by
  ext
  simpa only [Set.mem_star, mem_iff, not_iff_not] using star_mem_resolventSet_iff.symm

end Star

end ScalarSemiring

section ScalarRing

variable {R : Type u} {A : Type v}
variable [CommRing R] [Ring A] [Algebra R A]

local notation "Ïƒ" => spectrum R

local notation "â†‘â‚" => algebraMap R A

theorem subset_subalgebra {S R A : Type*} [CommSemiring R] [Ring A] [Algebra R A]
    [SetLike S A] [SubringClass S A] [SMulMemClass S R A] {s : S} (a : s) :
    spectrum R (a : A) âŠ† spectrum R a :=
  Set.compl_subset_compl.mpr fun _ â†¦ IsUnit.map (SubalgebraClass.val s)

theorem singleton_add_eq (a : A) (r : R) : {r} + Ïƒ a = Ïƒ (â†‘â‚ r + a) :=
  ext fun x => by
    rw [singleton_add, image_add_left, mem_preimage, add_comm, add_mem_iff, map_neg, neg_neg]

theorem add_singleton_eq (a : A) (r : R) : Ïƒ a + {r} = Ïƒ (a + â†‘â‚ r) :=
  add_comm {r} (Ïƒ a) â–¸ add_comm (algebraMap R A r) a â–¸ singleton_add_eq a r

theorem vadd_eq (a : A) (r : R) : r +áµ¥ Ïƒ a = Ïƒ (â†‘â‚ r + a) :=
  singleton_add.symm.trans <| singleton_add_eq a r

theorem neg_eq (a : A) : -Ïƒ a = Ïƒ (-a) :=
  Set.ext fun x => by
    simp only [mem_neg, mem_iff, map_neg, â† neg_add', IsUnit.neg_iff, sub_neg_eq_add]

theorem singleton_sub_eq (a : A) (r : R) : {r} - Ïƒ a = Ïƒ (â†‘â‚ r - a) := by
  rw [sub_eq_add_neg, neg_eq, singleton_add_eq, sub_eq_add_neg]

theorem sub_singleton_eq (a : A) (r : R) : Ïƒ a - {r} = Ïƒ (a - â†‘â‚ r) := by
  simpa only [neg_sub, neg_eq] using congr_arg Neg.neg (singleton_sub_eq a r)

end ScalarRing

section ScalarSemifield

variable {R : Type u} {A : Type v} [Semifield R] [Ring A] [Algebra R A]

@[simp]
lemma invâ‚€_mem_iff {r : R} {a : AË£} :
    râ»Â¹ âˆˆ spectrum R (a : A) â†” r âˆˆ spectrum R (â†‘aâ»Â¹ : A) := by
  obtain (rfl | hr) := eq_or_ne r 0
  Â· simp [zero_mem_iff]
  Â· lift r to RË£ using hr.isUnit
    simp [inv_mem_iff]

lemma invâ‚€_mem_inv_iff {r : R} {a : AË£} :
    râ»Â¹ âˆˆ spectrum R (â†‘aâ»Â¹ : A) â†” r âˆˆ spectrum R (a : A) := by
  simp

alias âŸ¨of_invâ‚€_mem, invâ‚€_memâŸ© := invâ‚€_mem_iff
alias âŸ¨of_invâ‚€_mem_inv, invâ‚€_mem_invâŸ© := invâ‚€_mem_inv_iff

end ScalarSemifield

section ScalarField

variable {ğ•œ : Type u} {A : Type v}
variable [Field ğ•œ] [Ring A] [Algebra ğ•œ A]

local notation "Ïƒ" => spectrum ğ•œ

local notation "â†‘â‚" => algebraMap ğ•œ A

/-- Without the assumption `Nontrivial A`, then `0 : A` would be invertible. -/
@[simp]
theorem zero_eq [Nontrivial A] : Ïƒ (0 : A) = {0} := by
  refine Set.Subset.antisymm ?_ (by simp [Algebra.algebraMap_eq_smul_one, mem_iff])
  rw [spectrum, Set.compl_subset_comm]
  intro k hk
  rw [Set.mem_compl_singleton_iff] at hk
  have : IsUnit (Units.mk0 k hk â€¢ (1 : A)) := IsUnit.smul (Units.mk0 k hk) isUnit_one
  simpa [mem_resolventSet_iff, Algebra.algebraMap_eq_smul_one]

@[simp]
theorem scalar_eq [Nontrivial A] (k : ğ•œ) : Ïƒ (â†‘â‚ k) = {k} := by
  rw [â† add_zero (â†‘â‚ k), â† singleton_add_eq, zero_eq, Set.singleton_add_singleton, add_zero]

@[simp]
theorem one_eq [Nontrivial A] : Ïƒ (1 : A) = {1} :=
  calc
    Ïƒ (1 : A) = Ïƒ (â†‘â‚ 1) := by rw [Algebra.algebraMap_eq_smul_one, one_smul]
    _ = {1} := scalar_eq 1

/-- the assumption `(Ïƒ a).Nonempty` is necessary and cannot be removed without
further conditions on the algebra `A` and scalar field `ğ•œ`. -/
theorem smul_eq_smul [Nontrivial A] (k : ğ•œ) (a : A) (ha : (Ïƒ a).Nonempty) :
    Ïƒ (k â€¢ a) = k â€¢ Ïƒ a := by
  rcases eq_or_ne k 0 with (rfl | h)
  Â· simpa [ha, zero_smul_set] using (show {(0 : ğ•œ)} = (0 : Set ğ•œ) from rfl)
  Â· exact unit_smul_eq_smul a (Units.mk0 k h)

theorem nonzero_mul_comm (a b : A) : Ïƒ (a * b) \ {0} = Ïƒ (b * a) \ {0} := by
  suffices h : âˆ€ x y : A, Ïƒ (x * y) \ {0} âŠ† Ïƒ (y * x) \ {0} from
    Set.eq_of_subset_of_subset (h a b) (h b a)
  rintro _ _ k âŸ¨k_mem, k_neqâŸ©
  change ((Units.mk0 k k_neq) : ğ•œ) âˆˆ _ at k_mem
  exact âŸ¨unit_mem_mul_comm.mp k_mem, k_neqâŸ©

protected theorem map_inv (a : AË£) : (Ïƒ (a : A))â»Â¹ = Ïƒ (â†‘aâ»Â¹ : A) := by
  ext
  simp

end ScalarField

end spectrum

namespace AlgHom

section CommSemiring

variable {F R A B : Type*} [CommSemiring R] [Ring A] [Algebra R A] [Ring B] [Algebra R B]
variable [FunLike F A B] [AlgHomClass F R A B]

local notation "Ïƒ" => spectrum R

local notation "â†‘â‚" => algebraMap R A

theorem mem_resolventSet_apply (Ï† : F) {a : A} {r : R} (h : r âˆˆ resolventSet R a) :
    r âˆˆ resolventSet R ((Ï† : A â†’ B) a) := by
  simpa only [map_sub, AlgHomClass.commutes] using h.map Ï†

theorem spectrum_apply_subset (Ï† : F) (a : A) : Ïƒ ((Ï† : A â†’ B) a) âŠ† Ïƒ a := fun _ =>
  mt (mem_resolventSet_apply Ï†)

end CommSemiring

section CommRing

variable {F R A : Type*} [CommRing R] [Ring A] [Algebra R A]
variable [FunLike F A R] [AlgHomClass F R A R]

local notation "Ïƒ" => spectrum R

local notation "â†‘â‚" => algebraMap R A

theorem apply_mem_spectrum [Nontrivial R] (Ï† : F) (a : A) : Ï† a âˆˆ Ïƒ a := by
  have h : â†‘â‚ (Ï† a) - a âˆˆ RingHom.ker (Ï† : A â†’+* R) := by
    simp only [RingHom.mem_ker, map_sub, RingHom.coe_coe, AlgHomClass.commutes,
      Algebra.algebraMap_self, RingHom.id_apply, sub_self]
  simp only [spectrum.mem_iff, â† mem_nonunits_iff,
    coe_subset_nonunits (RingHom.ker_ne_top (Ï† : A â†’+* R)) h]

end CommRing

end AlgHom

@[simp]
theorem AlgEquiv.spectrum_eq {F R A B : Type*} [CommSemiring R] [Ring A] [Ring B] [Algebra R A]
    [Algebra R B] [EquivLike F A B] [AlgEquivClass F R A B] (f : F) (a : A) :
    spectrum R (f a) = spectrum R a :=
  Set.Subset.antisymm (AlgHom.spectrum_apply_subset _ _) <| by
    simpa only [AlgEquiv.coe_algHom, AlgEquiv.coe_coe_symm_apply_coe_apply] using
      AlgHom.spectrum_apply_subset (f : A â‰ƒâ‚[R] B).symm (f a)

section ConjugateUnits

variable {R A : Type*} [CommSemiring R] [Ring A] [Algebra R A]

/-- Conjugation by a unit preserves the spectrum, inverse on right. -/
@[simp]
lemma spectrum.units_conjugate {a : A} {u : AË£} :
    spectrum R (u * a * uâ»Â¹) = spectrum R a := by
  suffices âˆ€ (b : A) (v : AË£), spectrum R (v * b * vâ»Â¹) âŠ† spectrum R b by
    refine le_antisymm (this a u) ?_
    apply le_of_eq_of_le ?_ <| this (u * a * uâ»Â¹) uâ»Â¹
    simp [mul_assoc]
  intro a u Î¼ hÎ¼
  rw [spectrum.mem_iff] at hÎ¼ âŠ¢
  contrapose! hÎ¼
  simpa [mul_sub, sub_mul, Algebra.right_comm] using u.isUnit.mul hÎ¼ |>.mul uâ»Â¹.isUnit

/-- Conjugation by a unit preserves the spectrum, inverse on left. -/
@[simp]
lemma spectrum.units_conjugate' {a : A} {u : AË£} :
    spectrum R (uâ»Â¹ * a * u) = spectrum R a := by
  simpa using spectrum.units_conjugate (u := uâ»Â¹)

end ConjugateUnits
