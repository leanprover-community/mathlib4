/-
Copyright (c) 2022 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.Algebra.Associated
import Mathlib.NumberTheory.Divisors

#align_import algebra.is_prime_pow from "leanprover-community/mathlib"@"f7fc89d5d5ff1db2d1242c7bb0e9062ce47ef47c"

/-!
# Prime powers

This file deals with prime powers: numbers which are positive integer powers of a single prime.
-/


variable {R : Type*} [CommMonoidWithZero R] (n p : R) (k : ‚Ñï)

/-- `n` is a prime power if there is a prime `p` and a positive natural `k` such that `n` can be
written as `p^k`. -/
def IsPrimePow : Prop :=
  ‚àÉ (p : R) (k : ‚Ñï), Prime p ‚àß 0 < k ‚àß p ^ k = n
#align is_prime_pow IsPrimePow

theorem isPrimePow_def : IsPrimePow n ‚Üî ‚àÉ (p : R) (k : ‚Ñï), Prime p ‚àß 0 < k ‚àß p ^ k = n :=
  Iff.rfl
#align is_prime_pow_def isPrimePow_def

/-- An equivalent definition for prime powers: `n` is a prime power iff there is a prime `p` and a
natural `k` such that `n` can be written as `p^(k+1)`. -/
theorem isPrimePow_iff_pow_succ : IsPrimePow n ‚Üî ‚àÉ (p : R) (k : ‚Ñï), Prime p ‚àß p ^ (k + 1) = n :=
  (isPrimePow_def _).trans
    ‚ü®fun ‚ü®p, k, hp, hk, hn‚ü© => ‚ü®_, _, hp, by rwa [Nat.sub_add_cancel hk]‚ü©, fun ‚ü®p, k, hp, hn‚ü© =>
                                             -- üéâ no goals
      ‚ü®_, _, hp, Nat.succ_pos', hn‚ü©‚ü©
#align is_prime_pow_iff_pow_succ isPrimePow_iff_pow_succ

theorem not_isPrimePow_zero [NoZeroDivisors R] : ¬¨IsPrimePow (0 : R) := by
  simp only [isPrimePow_def, not_exists, not_and', and_imp]
  -- ‚ä¢ ‚àÄ (x : R) (x_1 : ‚Ñï), 0 < x_1 ‚Üí x ^ x_1 = 0 ‚Üí ¬¨Prime x
  intro x n _hn hx
  -- ‚ä¢ ¬¨Prime x
  rw [pow_eq_zero hx]
  -- ‚ä¢ ¬¨Prime 0
  simp
  -- üéâ no goals
#align not_is_prime_pow_zero not_isPrimePow_zero

theorem IsPrimePow.not_unit {n : R} (h : IsPrimePow n) : ¬¨IsUnit n :=
  let ‚ü®_p, _k, hp, hk, hn‚ü© := h
  hn ‚ñ∏ (isUnit_pow_iff hk.ne').not.mpr hp.not_unit
#align is_prime_pow.not_unit IsPrimePow.not_unit

theorem IsUnit.not_isPrimePow {n : R} (h : IsUnit n) : ¬¨IsPrimePow n := fun h' => h'.not_unit h
#align is_unit.not_is_prime_pow IsUnit.not_isPrimePow

theorem not_isPrimePow_one : ¬¨IsPrimePow (1 : R) :=
  isUnit_one.not_isPrimePow
#align not_is_prime_pow_one not_isPrimePow_one

theorem Prime.isPrimePow {p : R} (hp : Prime p) : IsPrimePow p :=
  ‚ü®p, 1, hp, zero_lt_one, by simp‚ü©
                             -- üéâ no goals
#align prime.is_prime_pow Prime.isPrimePow

theorem IsPrimePow.pow {n : R} (hn : IsPrimePow n) {k : ‚Ñï} (hk : k ‚â† 0) : IsPrimePow (n ^ k) :=
  let ‚ü®p, k', hp, hk', hn‚ü© := hn
  ‚ü®p, k * k', hp, mul_pos hk.bot_lt hk', by rw [pow_mul', hn]‚ü©
                                            -- üéâ no goals
#align is_prime_pow.pow IsPrimePow.pow

theorem IsPrimePow.ne_zero [NoZeroDivisors R] {n : R} (h : IsPrimePow n) : n ‚â† 0 := fun t =>
  not_isPrimePow_zero (t ‚ñ∏ h)
#align is_prime_pow.ne_zero IsPrimePow.ne_zero

theorem IsPrimePow.ne_one {n : R} (h : IsPrimePow n) : n ‚â† 1 := fun t =>
  not_isPrimePow_one (t ‚ñ∏ h)
#align is_prime_pow.ne_one IsPrimePow.ne_one

section Nat

theorem isPrimePow_nat_iff (n : ‚Ñï) : IsPrimePow n ‚Üî ‚àÉ p k : ‚Ñï, Nat.Prime p ‚àß 0 < k ‚àß p ^ k = n := by
  simp only [isPrimePow_def, Nat.prime_iff]
  -- üéâ no goals
#align is_prime_pow_nat_iff isPrimePow_nat_iff

theorem Nat.Prime.isPrimePow {p : ‚Ñï} (hp : p.Prime) : IsPrimePow p :=
  _root_.Prime.isPrimePow (prime_iff.mp hp)
#align nat.prime.is_prime_pow Nat.Prime.isPrimePow

theorem isPrimePow_nat_iff_bounded (n : ‚Ñï) :
    IsPrimePow n ‚Üî ‚àÉ p : ‚Ñï, p ‚â§ n ‚àß ‚àÉ k : ‚Ñï, k ‚â§ n ‚àß p.Prime ‚àß 0 < k ‚àß p ^ k = n := by
  rw [isPrimePow_nat_iff]
  -- ‚ä¢ (‚àÉ p k, Nat.Prime p ‚àß 0 < k ‚àß p ^ k = n) ‚Üî ‚àÉ p, p ‚â§ n ‚àß ‚àÉ k, k ‚â§ n ‚àß Nat.Pri ‚Ä¶
  refine' Iff.symm ‚ü®fun ‚ü®p, _, k, _, hp, hk, hn‚ü© => ‚ü®p, k, hp, hk, hn‚ü©, _‚ü©
  -- ‚ä¢ (‚àÉ p k, Nat.Prime p ‚àß 0 < k ‚àß p ^ k = n) ‚Üí ‚àÉ p, p ‚â§ n ‚àß ‚àÉ k, k ‚â§ n ‚àß Nat.Pri ‚Ä¶
  rintro ‚ü®p, k, hp, hk, rfl‚ü©
  -- ‚ä¢ ‚àÉ p_1, p_1 ‚â§ p ^ k ‚àß ‚àÉ k_1, k_1 ‚â§ p ^ k ‚àß Nat.Prime p_1 ‚àß 0 < k_1 ‚àß p_1 ^ k_ ‚Ä¶
  refine' ‚ü®p, _, k, (Nat.lt_pow_self hp.one_lt _).le, hp, hk, rfl‚ü©
  -- ‚ä¢ p ‚â§ p ^ k
  conv => { lhs; rw [‚Üê(pow_one p)] }
  -- ‚ä¢ p ^ 1 ‚â§ p ^ k
  exact (Nat.pow_le_iff_le_right hp.two_le).mpr hk
  -- üéâ no goals
#align is_prime_pow_nat_iff_bounded isPrimePow_nat_iff_bounded

instance {n : ‚Ñï} : Decidable (IsPrimePow n) :=
  decidable_of_iff' _ (isPrimePow_nat_iff_bounded n)

theorem IsPrimePow.dvd {n m : ‚Ñï} (hn : IsPrimePow n) (hm : m ‚à£ n) (hm‚ÇÅ : m ‚â† 1) : IsPrimePow m := by
  rw [isPrimePow_nat_iff] at hn ‚ä¢
  -- ‚ä¢ ‚àÉ p k, Nat.Prime p ‚àß 0 < k ‚àß p ^ k = m
  rcases hn with ‚ü®p, k, hp, _hk, rfl‚ü©
  -- ‚ä¢ ‚àÉ p k, Nat.Prime p ‚àß 0 < k ‚àß p ^ k = m
  obtain ‚ü®i, hik, rfl‚ü© := (Nat.dvd_prime_pow hp).1 hm
  -- ‚ä¢ ‚àÉ p_1 k, Nat.Prime p_1 ‚àß 0 < k ‚àß p_1 ^ k = p ^ i
  refine' ‚ü®p, i, hp, _, rfl‚ü©
  -- ‚ä¢ 0 < i
  apply Nat.pos_of_ne_zero
  -- ‚ä¢ i ‚â† 0
  rintro rfl
  -- ‚ä¢ False
  simp only [pow_zero, ne_eq] at hm‚ÇÅ
  -- üéâ no goals
#align is_prime_pow.dvd IsPrimePow.dvd

theorem Nat.disjoint_divisors_filter_isPrimePow {a b : ‚Ñï} (hab : a.coprime b) :
    Disjoint (a.divisors.filter IsPrimePow) (b.divisors.filter IsPrimePow) := by
  simp only [Finset.disjoint_left, Finset.mem_filter, and_imp, Nat.mem_divisors, not_and]
  -- ‚ä¢ ‚àÄ ‚¶Éa_1 : ‚Ñï‚¶Ñ, a_1 ‚à£ a ‚Üí a ‚â† 0 ‚Üí IsPrimePow a_1 ‚Üí a_1 ‚à£ b ‚Üí b ‚â† 0 ‚Üí ¬¨IsPrimePo ‚Ä¶
  rintro n han _ha hn hbn _hb -
  -- ‚ä¢ False
  exact hn.ne_one (Nat.eq_one_of_dvd_coprimes hab han hbn)
  -- üéâ no goals
#align nat.disjoint_divisors_filter_prime_pow Nat.disjoint_divisors_filter_isPrimePow

theorem IsPrimePow.two_le : ‚àÄ {n : ‚Ñï}, IsPrimePow n ‚Üí 2 ‚â§ n
  | 0, h => (not_isPrimePow_zero h).elim
  | 1, h => (not_isPrimePow_one h).elim
  | _n + 2, _ => le_add_self
#align is_prime_pow.two_le IsPrimePow.two_le

theorem IsPrimePow.pos {n : ‚Ñï} (hn : IsPrimePow n) : 0 < n :=
  pos_of_gt hn.two_le
#align is_prime_pow.pos IsPrimePow.pos

theorem IsPrimePow.one_lt {n : ‚Ñï} (h : IsPrimePow n) : 1 < n :=
  h.two_le
#align is_prime_pow.one_lt IsPrimePow.one_lt

end Nat
