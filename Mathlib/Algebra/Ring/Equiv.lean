/-
Copyright (c) 2018 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Callum Sutton, Yury Kudryashov
-/
import Mathlib.Algebra.Field.Defs
import Mathlib.Algebra.Group.Opposite
import Mathlib.Algebra.Hom.Ring
import Mathlib.Logic.Equiv.Set
import Mathlib.Util.AssertExists
import Mathlib.Algebra.Hom.Equiv.Basic

#align_import algebra.ring.equiv from "leanprover-community/mathlib"@"00f91228655eecdcd3ac97a7fd8dbcb139fe990a"

/-!
# (Semi)ring equivs

In this file we define an extension of `Equiv` called `RingEquiv`, which is a datatype representing
an isomorphism of `Semiring`s, `Ring`s, `DivisionRing`s, or `Field`s.

## Notations

* ``infixl ` ‚âÉ+* `:25 := RingEquiv``

The extended equiv have coercions to functions, and the coercion is the canonical notation when
treating the isomorphism as maps.

## Implementation notes

The fields for `RingEquiv` now avoid the unbundled `isMulHom` and `isAddHom`, as these are
deprecated.

Definition of multiplication in the groups of automorphisms agrees with function composition,
multiplication in `Equiv.Perm`, and multiplication in `CategoryTheory.End`, not with
`CategoryTheory.CategoryStruct.comp`.

## Tags

Equiv, MulEquiv, AddEquiv, RingEquiv, MulAut, AddAut, RingAut
-/


variable {F Œ± Œ≤ R S S' : Type*}


/-- makes a `NonUnitalRingHom` from the bijective inverse of a `NonUnitalRingHom` -/
@[simps] def NonUnitalRingHom.inverse
    [NonUnitalNonAssocSemiring R] [NonUnitalNonAssocSemiring S]
    (f : R ‚Üí‚Çô+* S) (g : S ‚Üí R)
    (h‚ÇÅ : Function.LeftInverse g f) (h‚ÇÇ : Function.RightInverse g f) : S ‚Üí‚Çô+* R :=
  { (f : R ‚Üí+ S).inverse g h‚ÇÅ h‚ÇÇ, (f : R ‚Üí‚Çô* S).inverse g h‚ÇÅ h‚ÇÇ with toFun := g }

/-- makes a `RingHom` from the bijective inverse of a `RingHom` -/
@[simps] def RingHom.inverse [NonAssocSemiring R] [NonAssocSemiring S]
    (f : RingHom R S) (g : S ‚Üí R)
    (h‚ÇÅ : Function.LeftInverse g f) (h‚ÇÇ : Function.RightInverse g f) : S ‚Üí+* R :=
  { (f : OneHom R S).inverse g h‚ÇÅ,
    (f : MulHom R S).inverse g h‚ÇÅ h‚ÇÇ,
    (f : R ‚Üí+ S).inverse g h‚ÇÅ h‚ÇÇ with toFun := g }

/-- An equivalence between two (non-unital non-associative semi)rings that preserves the
algebraic structure. -/
structure RingEquiv (R S : Type*) [Mul R] [Mul S] [Add R] [Add S] extends R ‚âÉ S, R ‚âÉ* S, R ‚âÉ+ S
#align ring_equiv RingEquiv

-- mathport name: ¬´expr ‚âÉ+* ¬ª*
/-- Notation for `RingEquiv`. -/
infixl:25 " ‚âÉ+* " => RingEquiv

/-- The "plain" equivalence of types underlying an equivalence of (semi)rings. -/
add_decl_doc RingEquiv.toEquiv
#align ring_equiv.to_equiv RingEquiv.toEquiv

/-- The equivalence of additive monoids underlying an equivalence of (semi)rings. -/
add_decl_doc RingEquiv.toAddEquiv
#align ring_equiv.to_add_equiv RingEquiv.toAddEquiv

/-- The equivalence of multiplicative monoids underlying an equivalence of (semi)rings. -/
add_decl_doc RingEquiv.toMulEquiv
#align ring_equiv.to_mul_equiv RingEquiv.toMulEquiv

/-- `RingEquivClass F R S` states that `F` is a type of ring structure preserving equivalences.
You should extend this class when you extend `RingEquiv`. -/
class RingEquivClass (F : Type*) (R S : outParam (Type*)) [Mul R] [Add R] [Mul S] [Add S] extends
  MulEquivClass F R S where
  /-- By definition, a ring isomorphism preserves the additive structure. -/
  map_add : ‚àÄ (f : F) (a b), f (a + b) = f a + f b
#align ring_equiv_class RingEquivClass

namespace RingEquivClass

-- See note [lower instance priority]
instance (priority := 100) toAddEquivClass [Mul R] [Add R]
    [Mul S] [Add S] [h : RingEquivClass F R S] : AddEquivClass F R S :=
  { h with coe := h.coe }
#align ring_equiv_class.to_add_equiv_class RingEquivClass.toAddEquivClass

-- See note [lower instance priority]
instance (priority := 100) toRingHomClass [NonAssocSemiring R] [NonAssocSemiring S]
    [h : RingEquivClass F R S] : RingHomClass F R S :=
  { h with
    coe := h.coe
    coe_injective' := FunLike.coe_injective
    map_zero := map_zero
    map_one := map_one }
#align ring_equiv_class.to_ring_hom_class RingEquivClass.toRingHomClass

-- See note [lower instance priority]
instance (priority := 100) toNonUnitalRingHomClass [NonUnitalNonAssocSemiring R]
    [NonUnitalNonAssocSemiring S] [h : RingEquivClass F R S] : NonUnitalRingHomClass F R S :=
  { h with
    coe := h.coe
    coe_injective' := FunLike.coe_injective
    map_zero := map_zero }
#align ring_equiv_class.to_non_unital_ring_hom_class RingEquivClass.toNonUnitalRingHomClass

/-- Turn an element of a type `F` satisfying `RingEquivClass F Œ± Œ≤` into an actual
`RingEquiv`. This is declared as the default coercion from `F` to `Œ± ‚âÉ+* Œ≤`. -/
@[coe]
def toRingEquiv [Mul Œ±] [Add Œ±] [Mul Œ≤] [Add Œ≤] [RingEquivClass F Œ± Œ≤] (f : F) :
  Œ± ‚âÉ+* Œ≤ :=
{ (f : Œ± ‚âÉ* Œ≤), (f : Œ± ‚âÉ+ Œ≤) with }

end RingEquivClass

/-- Any type satisfying `RingEquivClass` can be cast into `RingEquiv` via
`RingEquivClass.toRingEquiv`. -/
instance [Mul Œ±] [Add Œ±] [Mul Œ≤] [Add Œ≤] [RingEquivClass F Œ± Œ≤] : CoeTC F (Œ± ‚âÉ+* Œ≤) :=
  ‚ü®RingEquivClass.toRingEquiv‚ü©

namespace RingEquiv

section Basic

variable [Mul R] [Mul S] [Add R] [Add S] [Mul S'] [Add S']

instance : RingEquivClass (R ‚âÉ+* S) R S where
  coe f := f.toFun
  inv f := f.invFun
  coe_injective' e f h‚ÇÅ h‚ÇÇ := by
    cases e
    -- ‚ä¢ { toEquiv := toEquiv‚úù, map_mul' := map_mul'‚úù, map_add' := map_add'‚úù } = f
    cases f
    -- ‚ä¢ { toEquiv := toEquiv‚úù¬π, map_mul' := map_mul'‚úù¬π, map_add' := map_add'‚úù¬π } = { ‚Ä¶
    congr
    -- ‚ä¢ toEquiv‚úù¬π = toEquiv‚úù
    apply Equiv.coe_fn_injective h‚ÇÅ
    -- üéâ no goals
  map_add := map_add'
  map_mul := map_mul'
  left_inv f := f.left_inv
  right_inv f := f.right_inv

@[simp]
theorem toEquiv_eq_coe (f : R ‚âÉ+* S) : f.toEquiv = f :=
  rfl
#align ring_equiv.to_equiv_eq_coe RingEquiv.toEquiv_eq_coe

-- Porting note: `toFun_eq_coe` no longer needed in Lean4
#noalign ring_equiv.to_fun_eq_coe

@[simp]
theorem coe_toEquiv (f : R ‚âÉ+* S) : ‚áë(f : R ‚âÉ S) = f :=
  rfl
#align ring_equiv.coe_to_equiv RingEquiv.coe_toEquiv

/-- A ring isomorphism preserves multiplication. -/
protected theorem map_mul (e : R ‚âÉ+* S) (x y : R) : e (x * y) = e x * e y :=
  map_mul e x y
#align ring_equiv.map_mul RingEquiv.map_mul

/-- A ring isomorphism preserves addition. -/
protected theorem map_add (e : R ‚âÉ+* S) (x y : R) : e (x + y) = e x + e y :=
  map_add e x y
#align ring_equiv.map_add RingEquiv.map_add

/-- Two ring isomorphisms agree if they are defined by the
    same underlying function. -/
@[ext]
theorem ext {f g : R ‚âÉ+* S} (h : ‚àÄ x, f x = g x) : f = g :=
  FunLike.ext f g h
#align ring_equiv.ext RingEquiv.ext

@[simp]
theorem coe_mk (e h‚ÇÉ h‚ÇÑ) : ‚áë(‚ü®e, h‚ÇÉ, h‚ÇÑ‚ü© : R ‚âÉ+* S) = e :=
  rfl
#align ring_equiv.coe_mk RingEquiv.coe_mk‚Çì

-- Porting note: `toEquiv_mk` no longer needed in Lean4
#noalign ring_equiv.to_equiv_mk

@[simp]
theorem mk_coe (e : R ‚âÉ+* S) (e' h‚ÇÅ h‚ÇÇ h‚ÇÉ h‚ÇÑ) : (‚ü®‚ü®e, e', h‚ÇÅ, h‚ÇÇ‚ü©, h‚ÇÉ, h‚ÇÑ‚ü© : R ‚âÉ+* S) = e :=
  ext fun _ => rfl
#align ring_equiv.mk_coe RingEquiv.mk_coe

protected theorem congr_arg {f : R ‚âÉ+* S} {x x' : R} : x = x' ‚Üí f x = f x' :=
  FunLike.congr_arg f
#align ring_equiv.congr_arg RingEquiv.congr_arg

protected theorem congr_fun {f g : R ‚âÉ+* S} (h : f = g) (x : R) : f x = g x :=
  FunLike.congr_fun h x
#align ring_equiv.congr_fun RingEquiv.congr_fun

protected theorem ext_iff {f g : R ‚âÉ+* S} : f = g ‚Üî ‚àÄ x, f x = g x :=
  FunLike.ext_iff
#align ring_equiv.ext_iff RingEquiv.ext_iff

@[simp]
theorem toAddEquiv_eq_coe (f : R ‚âÉ+* S) : f.toAddEquiv = ‚Üëf :=
  rfl
#align ring_equiv.to_add_equiv_eq_coe RingEquiv.toAddEquiv_eq_coe

@[simp]
theorem toMulEquiv_eq_coe (f : R ‚âÉ+* S) : f.toMulEquiv = ‚Üëf :=
  rfl
#align ring_equiv.to_mul_equiv_eq_coe RingEquiv.toMulEquiv_eq_coe

@[simp, norm_cast]
theorem coe_toMulEquiv (f : R ‚âÉ+* S) : ‚áë(f : R ‚âÉ* S) = f :=
  rfl
#align ring_equiv.coe_to_mul_equiv RingEquiv.coe_toMulEquiv

@[simp]
theorem coe_toAddEquiv (f : R ‚âÉ+* S) : ‚áë(f : R ‚âÉ+ S) = f :=
  rfl
#align ring_equiv.coe_to_add_equiv RingEquiv.coe_toAddEquiv

/-- The `RingEquiv` between two semirings with a unique element. -/
def ringEquivOfUnique {M N} [Unique M] [Unique N] [Add M] [Mul M] [Add N] [Mul N] : M ‚âÉ+* N :=
  { AddEquiv.addEquivOfUnique, MulEquiv.mulEquivOfUnique with }
#align ring_equiv.ring_equiv_of_unique RingEquiv.ringEquivOfUnique

instance {M N} [Unique M] [Unique N] [Add M] [Mul M] [Add N] [Mul N] :
    Unique (M ‚âÉ+* N) where
  default := ringEquivOfUnique
  uniq _ := ext fun _ => Subsingleton.elim _ _

variable (R)

/-- The identity map is a ring isomorphism. -/
@[refl]
def refl : R ‚âÉ+* R :=
  { MulEquiv.refl R, AddEquiv.refl R with }
#align ring_equiv.refl RingEquiv.refl

@[simp]
theorem refl_apply (x : R) : RingEquiv.refl R x = x :=
  rfl
#align ring_equiv.refl_apply RingEquiv.refl_apply

@[simp]
theorem coe_addEquiv_refl : (RingEquiv.refl R : R ‚âÉ+ R) = AddEquiv.refl R :=
  rfl
#align ring_equiv.coe_add_equiv_refl RingEquiv.coe_addEquiv_refl

@[simp]
theorem coe_mulEquiv_refl : (RingEquiv.refl R : R ‚âÉ* R) = MulEquiv.refl R :=
  rfl
#align ring_equiv.coe_mul_equiv_refl RingEquiv.coe_mulEquiv_refl

instance : Inhabited (R ‚âÉ+* R) :=
  ‚ü®RingEquiv.refl R‚ü©

variable {R}

/-- The inverse of a ring isomorphism is a ring isomorphism. -/
@[symm]
protected def symm (e : R ‚âÉ+* S) : S ‚âÉ+* R :=
  { e.toMulEquiv.symm, e.toAddEquiv.symm with }
#align ring_equiv.symm RingEquiv.symm

/-- See Note [custom simps projection] -/
def Simps.symm_apply (e : R ‚âÉ+* S) : S ‚Üí R :=
  e.symm
#align ring_equiv.simps.symm_apply RingEquiv.Simps.symm_apply

initialize_simps_projections RingEquiv (toFun ‚Üí apply, invFun ‚Üí symm_apply)

@[simp]
theorem invFun_eq_symm (f : R ‚âÉ+* S) : EquivLike.inv f = f.symm :=
  rfl
#align ring_equiv.inv_fun_eq_symm RingEquiv.invFun_eq_symm

@[simp]
theorem symm_symm (e : R ‚âÉ+* S) : e.symm.symm = e :=
  ext fun _ => rfl
#align ring_equiv.symm_symm RingEquiv.symm_symm

--Porting note: new theorem
@[simp]
theorem symm_refl : (RingEquiv.refl R).symm = RingEquiv.refl R :=
  rfl

@[simp]
theorem coe_toEquiv_symm (e : R ‚âÉ+* S) : (e.symm : S ‚âÉ R) = (e : R ‚âÉ S).symm :=
  rfl
#align ring_equiv.coe_to_equiv_symm RingEquiv.coe_toEquiv_symm

theorem symm_bijective : Function.Bijective (RingEquiv.symm : R ‚âÉ+* S ‚Üí S ‚âÉ+* R) :=
  Equiv.bijective ‚ü®RingEquiv.symm, RingEquiv.symm, symm_symm, symm_symm‚ü©
#align ring_equiv.symm_bijective RingEquiv.symm_bijective

@[simp]
theorem mk_coe' (e : R ‚âÉ+* S) (f h‚ÇÅ h‚ÇÇ h‚ÇÉ h‚ÇÑ) :
    (‚ü®‚ü®f, ‚áëe, h‚ÇÅ, h‚ÇÇ‚ü©, h‚ÇÉ, h‚ÇÑ‚ü© : S ‚âÉ+* R) = e.symm :=
  symm_bijective.injective <| ext fun _ => rfl
#align ring_equiv.mk_coe' RingEquiv.mk_coe'

@[simp]
theorem symm_mk (f : R ‚Üí S) (g h‚ÇÅ h‚ÇÇ h‚ÇÉ h‚ÇÑ) :
    (mk ‚ü®f, g, h‚ÇÅ, h‚ÇÇ‚ü© h‚ÇÉ h‚ÇÑ).symm =
      { (mk ‚ü®f, g, h‚ÇÅ, h‚ÇÇ‚ü© h‚ÇÉ h‚ÇÑ).symm with
        toFun := g
        invFun := f } :=
  rfl
#align ring_equiv.symm_mk RingEquiv.symm_mk

/-- Transitivity of `RingEquiv`. -/
@[trans]
protected def trans (e‚ÇÅ : R ‚âÉ+* S) (e‚ÇÇ : S ‚âÉ+* S') : R ‚âÉ+* S' :=
  { e‚ÇÅ.toMulEquiv.trans e‚ÇÇ.toMulEquiv, e‚ÇÅ.toAddEquiv.trans e‚ÇÇ.toAddEquiv with }
#align ring_equiv.trans RingEquiv.trans

theorem trans_apply (e‚ÇÅ : R ‚âÉ+* S) (e‚ÇÇ : S ‚âÉ+* S') (a : R) : e‚ÇÅ.trans e‚ÇÇ a = e‚ÇÇ (e‚ÇÅ a) :=
  rfl
#align ring_equiv.trans_apply RingEquiv.trans_apply

@[simp]
theorem coe_trans (e‚ÇÅ : R ‚âÉ+* S) (e‚ÇÇ : S ‚âÉ+* S') : (e‚ÇÅ.trans e‚ÇÇ : R ‚Üí S') = e‚ÇÇ ‚àò e‚ÇÅ :=
  rfl
#align ring_equiv.coe_trans RingEquiv.coe_trans

@[simp]
theorem symm_trans_apply (e‚ÇÅ : R ‚âÉ+* S) (e‚ÇÇ : S ‚âÉ+* S') (a : S') :
    (e‚ÇÅ.trans e‚ÇÇ).symm a = e‚ÇÅ.symm (e‚ÇÇ.symm a) :=
  rfl
#align ring_equiv.symm_trans_apply RingEquiv.symm_trans_apply

theorem symm_trans (e‚ÇÅ : R ‚âÉ+* S) (e‚ÇÇ : S ‚âÉ+* S') : (e‚ÇÅ.trans e‚ÇÇ).symm = e‚ÇÇ.symm.trans e‚ÇÅ.symm :=
  rfl
#align ring_equiv.symm_trans RingEquiv.symm_trans

protected theorem bijective (e : R ‚âÉ+* S) : Function.Bijective e :=
  EquivLike.bijective e
#align ring_equiv.bijective RingEquiv.bijective

protected theorem injective (e : R ‚âÉ+* S) : Function.Injective e :=
  EquivLike.injective e
#align ring_equiv.injective RingEquiv.injective

protected theorem surjective (e : R ‚âÉ+* S) : Function.Surjective e :=
  EquivLike.surjective e
#align ring_equiv.surjective RingEquiv.surjective

@[simp]
theorem apply_symm_apply (e : R ‚âÉ+* S) : ‚àÄ x, e (e.symm x) = x :=
  e.toEquiv.apply_symm_apply
#align ring_equiv.apply_symm_apply RingEquiv.apply_symm_apply

@[simp]
theorem symm_apply_apply (e : R ‚âÉ+* S) : ‚àÄ x, e.symm (e x) = x :=
  e.toEquiv.symm_apply_apply
#align ring_equiv.symm_apply_apply RingEquiv.symm_apply_apply

theorem image_eq_preimage (e : R ‚âÉ+* S) (s : Set R) : e '' s = e.symm ‚Åª¬π' s :=
  e.toEquiv.image_eq_preimage s
#align ring_equiv.image_eq_preimage RingEquiv.image_eq_preimage

@[simp]
theorem coe_mulEquiv_trans (e‚ÇÅ : R ‚âÉ+* S) (e‚ÇÇ : S ‚âÉ+* S') :
    (e‚ÇÅ.trans e‚ÇÇ : R ‚âÉ* S') = (e‚ÇÅ : R ‚âÉ* S).trans ‚Üëe‚ÇÇ :=
  rfl
#align ring_equiv.coe_mul_equiv_trans RingEquiv.coe_mulEquiv_trans

@[simp]
theorem coe_addEquiv_trans (e‚ÇÅ : R ‚âÉ+* S) (e‚ÇÇ : S ‚âÉ+* S') :
    (e‚ÇÅ.trans e‚ÇÇ : R ‚âÉ+ S') = (e‚ÇÅ : R ‚âÉ+ S).trans ‚Üëe‚ÇÇ :=
  rfl
#align ring_equiv.coe_add_equiv_trans RingEquiv.coe_addEquiv_trans

end Basic

section Opposite

open MulOpposite

/-- A ring iso `Œ± ‚âÉ+* Œ≤` can equivalently be viewed as a ring iso `Œ±·µê·µí·µñ ‚âÉ+* Œ≤·µê·µí·µñ`. -/
@[simps! symm_apply_apply symm_apply_symm_apply apply_apply apply_symm_apply]
protected def op {Œ± Œ≤} [Add Œ±] [Mul Œ±] [Add Œ≤] [Mul Œ≤] :
    Œ± ‚âÉ+* Œ≤ ‚âÉ (Œ±·µê·µí·µñ ‚âÉ+* Œ≤·µê·µí·µñ) where
  toFun f := { AddEquiv.mulOp f.toAddEquiv, MulEquiv.op f.toMulEquiv with }
  invFun f := { AddEquiv.mulOp.symm f.toAddEquiv, MulEquiv.op.symm f.toMulEquiv with }
  left_inv f := by
    ext
    -- ‚ä¢ ‚Üë((fun f =>
    rfl
    -- üéâ no goals
  right_inv f := by
    ext
    -- ‚ä¢ ‚Üë((fun f =>
    rfl
    -- üéâ no goals
#align ring_equiv.op RingEquiv.op
#align ring_equiv.op_symm_apply_apply RingEquiv.op_symm_apply_apply
#align ring_equiv.op_symm_apply_symm_apply RingEquiv.op_symm_apply_symm_apply

/-- The 'unopposite' of a ring iso `Œ±·µê·µí·µñ ‚âÉ+* Œ≤·µê·µí·µñ`. Inverse to `RingEquiv.op`. -/
@[simp]
protected def unop {Œ± Œ≤} [Add Œ±] [Mul Œ±] [Add Œ≤] [Mul Œ≤] : Œ±·µê·µí·µñ ‚âÉ+* Œ≤·µê·µí·µñ ‚âÉ (Œ± ‚âÉ+* Œ≤) :=
  RingEquiv.op.symm
#align ring_equiv.unop RingEquiv.unop

/-- A ring is isomorphic to the opposite of its opposite. -/
@[simps!]
def opOp (R : Type*) [Add R] [Mul R] : R ‚âÉ+* R·µê·µí·µñ·µê·µí·µñ where
  __ := MulEquiv.opOp R
  map_add' _ _ := rfl

section NonUnitalCommSemiring

variable (R) [NonUnitalCommSemiring R]

/-- A non-unital commutative ring is isomorphic to its opposite. -/
def toOpposite : R ‚âÉ+* R·µê·µí·µñ :=
  { MulOpposite.opEquiv with
    map_add' := fun _ _ => rfl
    map_mul' := fun x y => mul_comm (op y) (op x) }
#align ring_equiv.to_opposite RingEquiv.toOpposite

@[simp]
theorem toOpposite_apply (r : R) : toOpposite R r = op r :=
  rfl
#align ring_equiv.to_opposite_apply RingEquiv.toOpposite_apply

@[simp]
theorem toOpposite_symm_apply (r : R·µê·µí·µñ) : (toOpposite R).symm r = unop r :=
  rfl
#align ring_equiv.to_opposite_symm_apply RingEquiv.toOpposite_symm_apply

end NonUnitalCommSemiring

end Opposite

section NonUnitalSemiring

variable [NonUnitalNonAssocSemiring R] [NonUnitalNonAssocSemiring S] (f : R ‚âÉ+* S) (x y : R)

/-- A ring isomorphism sends zero to zero. -/
protected theorem map_zero : f 0 = 0 :=
  map_zero f
#align ring_equiv.map_zero RingEquiv.map_zero

variable {x}

protected theorem map_eq_zero_iff : f x = 0 ‚Üî x = 0 :=
  AddEquivClass.map_eq_zero_iff f
#align ring_equiv.map_eq_zero_iff RingEquiv.map_eq_zero_iff

theorem map_ne_zero_iff : f x ‚â† 0 ‚Üî x ‚â† 0 :=
  AddEquivClass.map_ne_zero_iff f
#align ring_equiv.map_ne_zero_iff RingEquiv.map_ne_zero_iff

/-- Produce a ring isomorphism from a bijective ring homomorphism. -/
noncomputable def ofBijective [NonUnitalRingHomClass F R S] (f : F) (hf : Function.Bijective f) :
    R ‚âÉ+* S :=
  { Equiv.ofBijective f hf with
    map_mul' := map_mul f
    map_add' := map_add f }
#align ring_equiv.of_bijective RingEquiv.ofBijective

@[simp]
theorem coe_ofBijective [NonUnitalRingHomClass F R S] (f : F) (hf : Function.Bijective f) :
    (ofBijective f hf : R ‚Üí S) = f :=
  rfl
#align ring_equiv.coe_of_bijective RingEquiv.coe_ofBijective

theorem ofBijective_apply [NonUnitalRingHomClass F R S] (f : F) (hf : Function.Bijective f)
    (x : R) : ofBijective f hf x = f x :=
  rfl
#align ring_equiv.of_bijective_apply RingEquiv.ofBijective_apply

/-- A family of ring isomorphisms `‚àÄ j, (R j ‚âÉ+* S j)` generates a
ring isomorphisms between `‚àÄ j, R j` and `‚àÄ j, S j`.

This is the `RingEquiv` version of `Equiv.piCongrRight`, and the dependent version of
`RingEquiv.arrowCongr`.
-/
@[simps apply]
def piCongrRight {Œπ : Type*} {R S : Œπ ‚Üí Type*} [‚àÄ i, NonUnitalNonAssocSemiring (R i)]
    [‚àÄ i, NonUnitalNonAssocSemiring (S i)] (e : ‚àÄ i, R i ‚âÉ+* S i) : (‚àÄ i, R i) ‚âÉ+* ‚àÄ i, S i :=
  { @MulEquiv.piCongrRight Œπ R S _ _ fun i => (e i).toMulEquiv,
    @AddEquiv.piCongrRight Œπ R S _ _ fun i => (e i).toAddEquiv with
    toFun := fun x j => e j (x j)
    invFun := fun x j => (e j).symm (x j) }
#align ring_equiv.Pi_congr_right RingEquiv.piCongrRight
#align ring_equiv.Pi_congr_right_apply RingEquiv.piCongrRight_apply

@[simp]
theorem piCongrRight_refl {Œπ : Type*} {R : Œπ ‚Üí Type*} [‚àÄ i, NonUnitalNonAssocSemiring (R i)] :
    (piCongrRight fun i => RingEquiv.refl (R i)) = RingEquiv.refl _ :=
  rfl
#align ring_equiv.Pi_congr_right_refl RingEquiv.piCongrRight_refl

@[simp]
theorem piCongrRight_symm {Œπ : Type*} {R S : Œπ ‚Üí Type*} [‚àÄ i, NonUnitalNonAssocSemiring (R i)]
    [‚àÄ i, NonUnitalNonAssocSemiring (S i)] (e : ‚àÄ i, R i ‚âÉ+* S i) :
    (piCongrRight e).symm = piCongrRight fun i => (e i).symm :=
  rfl
#align ring_equiv.Pi_congr_right_symm RingEquiv.piCongrRight_symm

@[simp]
theorem piCongrRight_trans {Œπ : Type*} {R S T : Œπ ‚Üí Type*}
    [‚àÄ i, NonUnitalNonAssocSemiring (R i)] [‚àÄ i, NonUnitalNonAssocSemiring (S i)]
    [‚àÄ i, NonUnitalNonAssocSemiring (T i)] (e : ‚àÄ i, R i ‚âÉ+* S i) (f : ‚àÄ i, S i ‚âÉ+* T i) :
    (piCongrRight e).trans (piCongrRight f) = piCongrRight fun i => (e i).trans (f i) :=
  rfl
#align ring_equiv.Pi_congr_right_trans RingEquiv.piCongrRight_trans

end NonUnitalSemiring

section Semiring

variable [NonAssocSemiring R] [NonAssocSemiring S] (f : R ‚âÉ+* S) (x y : R)

/-- A ring isomorphism sends one to one. -/
protected theorem map_one : f 1 = 1 :=
  map_one f
#align ring_equiv.map_one RingEquiv.map_one

variable {x}

protected theorem map_eq_one_iff : f x = 1 ‚Üî x = 1 :=
  MulEquivClass.map_eq_one_iff f
#align ring_equiv.map_eq_one_iff RingEquiv.map_eq_one_iff

theorem map_ne_one_iff : f x ‚â† 1 ‚Üî x ‚â† 1 :=
  MulEquivClass.map_ne_one_iff f
#align ring_equiv.map_ne_one_iff RingEquiv.map_ne_one_iff

theorem coe_monoidHom_refl : (RingEquiv.refl R : R ‚Üí* R) = MonoidHom.id R :=
  rfl
#align ring_equiv.coe_monoid_hom_refl RingEquiv.coe_monoidHom_refl

@[simp]
theorem coe_addMonoidHom_refl : (RingEquiv.refl R : R ‚Üí+ R) = AddMonoidHom.id R :=
  rfl
#align ring_equiv.coe_add_monoid_hom_refl RingEquiv.coe_addMonoidHom_refl

/-! `RingEquiv.coe_mulEquiv_refl` and `RingEquiv.coe_addEquiv_refl` are proved above
in higher generality -/


@[simp]
theorem coe_ringHom_refl : (RingEquiv.refl R : R ‚Üí+* R) = RingHom.id R :=
  rfl
#align ring_equiv.coe_ring_hom_refl RingEquiv.coe_ringHom_refl

@[simp]
theorem coe_monoidHom_trans [NonAssocSemiring S'] (e‚ÇÅ : R ‚âÉ+* S) (e‚ÇÇ : S ‚âÉ+* S') :
    (e‚ÇÅ.trans e‚ÇÇ : R ‚Üí* S') = (e‚ÇÇ : S ‚Üí* S').comp ‚Üëe‚ÇÅ :=
  rfl
#align ring_equiv.coe_monoid_hom_trans RingEquiv.coe_monoidHom_trans

@[simp]
theorem coe_addMonoidHom_trans [NonAssocSemiring S'] (e‚ÇÅ : R ‚âÉ+* S) (e‚ÇÇ : S ‚âÉ+* S') :
    (e‚ÇÅ.trans e‚ÇÇ : R ‚Üí+ S') = (e‚ÇÇ : S ‚Üí+ S').comp ‚Üëe‚ÇÅ :=
  rfl
#align ring_equiv.coe_add_monoid_hom_trans RingEquiv.coe_addMonoidHom_trans

/-! `RingEquiv.coe_mulEquiv_trans` and `RingEquiv.coe_addEquiv_trans` are proved above
in higher generality -/

@[simp]
theorem coe_ringHom_trans [NonAssocSemiring S'] (e‚ÇÅ : R ‚âÉ+* S) (e‚ÇÇ : S ‚âÉ+* S') :
    (e‚ÇÅ.trans e‚ÇÇ : R ‚Üí+* S') = (e‚ÇÇ : S ‚Üí+* S').comp ‚Üëe‚ÇÅ :=
  rfl
#align ring_equiv.coe_ring_hom_trans RingEquiv.coe_ringHom_trans

@[simp]
theorem comp_symm (e : R ‚âÉ+* S) : (e : R ‚Üí+* S).comp (e.symm : S ‚Üí+* R) = RingHom.id S :=
  RingHom.ext e.apply_symm_apply
#align ring_equiv.comp_symm RingEquiv.comp_symm

@[simp]
theorem symm_comp (e : R ‚âÉ+* S) : (e.symm : S ‚Üí+* R).comp (e : R ‚Üí+* S) = RingHom.id R :=
  RingHom.ext e.symm_apply_apply
#align ring_equiv.symm_comp RingEquiv.symm_comp

end Semiring

section NonUnitalRing

variable [NonUnitalNonAssocRing R] [NonUnitalNonAssocRing S] (f : R ‚âÉ+* S) (x y : R)

protected theorem map_neg : f (-x) = -f x :=
  map_neg f x
#align ring_equiv.map_neg RingEquiv.map_neg

protected theorem map_sub : f (x - y) = f x - f y :=
  map_sub f x y
#align ring_equiv.map_sub RingEquiv.map_sub

end NonUnitalRing

section Ring

variable [NonAssocRing R] [NonAssocRing S] (f : R ‚âÉ+* S) (x y : R)

-- Porting note: `simp` can now prove that, so we remove the `@[simp]` tag
theorem map_neg_one : f (-1) = -1 :=
  f.map_one ‚ñ∏ f.map_neg 1
#align ring_equiv.map_neg_one RingEquiv.map_neg_one

theorem map_eq_neg_one_iff {x : R} : f x = -1 ‚Üî x = -1 := by
  rw [‚Üê neg_eq_iff_eq_neg, ‚Üê neg_eq_iff_eq_neg, ‚Üê map_neg, RingEquiv.map_eq_one_iff]
  -- üéâ no goals
#align ring_equiv.map_eq_neg_one_iff RingEquiv.map_eq_neg_one_iff

end Ring

section NonUnitalSemiringHom

variable [NonUnitalNonAssocSemiring R] [NonUnitalNonAssocSemiring S] [NonUnitalNonAssocSemiring S']

/-- Reinterpret a ring equivalence as a non-unital ring homomorphism. -/
def toNonUnitalRingHom (e : R ‚âÉ+* S) : R ‚Üí‚Çô+* S :=
  { e.toMulEquiv.toMulHom, e.toAddEquiv.toAddMonoidHom with }
#align ring_equiv.to_non_unital_ring_hom RingEquiv.toNonUnitalRingHom

theorem toNonUnitalRingHom_injective :
    Function.Injective (toNonUnitalRingHom : R ‚âÉ+* S ‚Üí R ‚Üí‚Çô+* S) := fun _ _ h =>
  RingEquiv.ext (NonUnitalRingHom.ext_iff.1 h)
#align ring_equiv.to_non_unital_ring_hom_injective RingEquiv.toNonUnitalRingHom_injective

theorem toNonUnitalRingHom_eq_coe (f : R ‚âÉ+* S) : f.toNonUnitalRingHom = ‚Üëf :=
  rfl
#align ring_equiv.to_non_unital_ring_hom_eq_coe RingEquiv.toNonUnitalRingHom_eq_coe

@[simp, norm_cast]
theorem coe_toNonUnitalRingHom (f : R ‚âÉ+* S) : ‚áë(f : R ‚Üí‚Çô+* S) = f :=
  rfl
#align ring_equiv.coe_to_non_unital_ring_hom RingEquiv.coe_toNonUnitalRingHom

theorem coe_nonUnitalRingHom_inj_iff {R S : Type*} [NonUnitalNonAssocSemiring R]
    [NonUnitalNonAssocSemiring S] (f g : R ‚âÉ+* S) : f = g ‚Üî (f : R ‚Üí‚Çô+* S) = g :=
  ‚ü®fun h => by rw [h], fun h => ext <| NonUnitalRingHom.ext_iff.mp h‚ü©
               -- üéâ no goals
#align ring_equiv.coe_non_unital_ring_hom_inj_iff RingEquiv.coe_nonUnitalRingHom_inj_iff

@[simp]
theorem toNonUnitalRingHom_refl :
    (RingEquiv.refl R).toNonUnitalRingHom = NonUnitalRingHom.id R :=
  rfl
#align ring_equiv.to_non_unital_ring_hom_refl RingEquiv.toNonUnitalRingHom_refl

@[simp]
theorem toNonUnitalRingHom_apply_symm_toNonUnitalRingHom_apply (e : R ‚âÉ+* S) :
    ‚àÄ y : S, e.toNonUnitalRingHom (e.symm.toNonUnitalRingHom y) = y :=
  e.toEquiv.apply_symm_apply
#align ring_equiv.to_non_unital_ring_hom_apply_symm_to_non_unital_ring_hom_apply RingEquiv.toNonUnitalRingHom_apply_symm_toNonUnitalRingHom_apply

@[simp]
theorem symm_toNonUnitalRingHom_apply_toNonUnitalRingHom_apply (e : R ‚âÉ+* S) :
    ‚àÄ x : R, e.symm.toNonUnitalRingHom (e.toNonUnitalRingHom x) = x :=
  Equiv.symm_apply_apply e.toEquiv
#align ring_equiv.symm_to_non_unital_ring_hom_apply_to_non_unital_ring_hom_apply RingEquiv.symm_toNonUnitalRingHom_apply_toNonUnitalRingHom_apply

@[simp]
theorem toNonUnitalRingHom_trans (e‚ÇÅ : R ‚âÉ+* S) (e‚ÇÇ : S ‚âÉ+* S') :
    (e‚ÇÅ.trans e‚ÇÇ).toNonUnitalRingHom = e‚ÇÇ.toNonUnitalRingHom.comp e‚ÇÅ.toNonUnitalRingHom :=
  rfl
#align ring_equiv.to_non_unital_ring_hom_trans RingEquiv.toNonUnitalRingHom_trans

@[simp]
theorem toNonUnitalRingHomm_comp_symm_toNonUnitalRingHom (e : R ‚âÉ+* S) :
    e.toNonUnitalRingHom.comp e.symm.toNonUnitalRingHom = NonUnitalRingHom.id _ := by
  ext
  -- ‚ä¢ ‚Üë(NonUnitalRingHom.comp (toNonUnitalRingHom e) (toNonUnitalRingHom (RingEqui ‚Ä¶
  simp
  -- üéâ no goals
#align ring_equiv.to_non_unital_ring_hom_comp_symm_to_non_unital_ring_hom RingEquiv.toNonUnitalRingHomm_comp_symm_toNonUnitalRingHom

@[simp]
theorem symm_toNonUnitalRingHom_comp_toNonUnitalRingHom (e : R ‚âÉ+* S) :
    e.symm.toNonUnitalRingHom.comp e.toNonUnitalRingHom = NonUnitalRingHom.id _ := by
  ext
  -- ‚ä¢ ‚Üë(NonUnitalRingHom.comp (toNonUnitalRingHom (RingEquiv.symm e)) (toNonUnital ‚Ä¶
  simp
  -- üéâ no goals
#align ring_equiv.symm_to_non_unital_ring_hom_comp_to_non_unital_ring_hom RingEquiv.symm_toNonUnitalRingHom_comp_toNonUnitalRingHom

end NonUnitalSemiringHom

section SemiringHom

variable [NonAssocSemiring R] [NonAssocSemiring S] [NonAssocSemiring S']

/-- Reinterpret a ring equivalence as a ring homomorphism. -/
def toRingHom (e : R ‚âÉ+* S) : R ‚Üí+* S :=
  { e.toMulEquiv.toMonoidHom, e.toAddEquiv.toAddMonoidHom with }
#align ring_equiv.to_ring_hom RingEquiv.toRingHom

theorem toRingHom_injective : Function.Injective (toRingHom : R ‚âÉ+* S ‚Üí R ‚Üí+* S) := fun _ _ h =>
  RingEquiv.ext (RingHom.ext_iff.1 h)
#align ring_equiv.to_ring_hom_injective RingEquiv.toRingHom_injective

@[simp] theorem toRingHom_eq_coe (f : R ‚âÉ+* S) : f.toRingHom = ‚Üëf :=
  rfl
#align ring_equiv.to_ring_hom_eq_coe RingEquiv.toRingHom_eq_coe

@[simp, norm_cast]
theorem coe_toRingHom (f : R ‚âÉ+* S) : ‚áë(f : R ‚Üí+* S) = f :=
  rfl
#align ring_equiv.coe_to_ring_hom RingEquiv.coe_toRingHom

theorem coe_ringHom_inj_iff {R S : Type*} [NonAssocSemiring R] [NonAssocSemiring S]
    (f g : R ‚âÉ+* S) : f = g ‚Üî (f : R ‚Üí+* S) = g :=
  ‚ü®fun h => by rw [h], fun h => ext <| RingHom.ext_iff.mp h‚ü©
               -- üéâ no goals
#align ring_equiv.coe_ring_hom_inj_iff RingEquiv.coe_ringHom_inj_iff

/-- The two paths coercion can take to a `NonUnitalRingEquiv` are equivalent -/
@[simp, norm_cast]
theorem toNonUnitalRingHom_commutes (f : R ‚âÉ+* S) :
    ((f : R ‚Üí+* S) : R ‚Üí‚Çô+* S) = (f : R ‚Üí‚Çô+* S) :=
  rfl
#align ring_equiv.to_non_unital_ring_hom_commutes RingEquiv.toNonUnitalRingHom_commutes

/-- Reinterpret a ring equivalence as a monoid homomorphism. -/
abbrev toMonoidHom (e : R ‚âÉ+* S) : R ‚Üí* S :=
  e.toRingHom.toMonoidHom
#align ring_equiv.to_monoid_hom RingEquiv.toMonoidHom

/-- Reinterpret a ring equivalence as an `AddMonoid` homomorphism. -/
abbrev toAddMonoidHom (e : R ‚âÉ+* S) : R ‚Üí+ S :=
  e.toRingHom.toAddMonoidHom
#align ring_equiv.to_add_monoid_hom RingEquiv.toAddMonoidHom

/-- The two paths coercion can take to an `AddMonoidHom` are equivalent -/
theorem toAddMonoidMom_commutes (f : R ‚âÉ+* S) :
    (f : R ‚Üí+* S).toAddMonoidHom = (f : R ‚âÉ+ S).toAddMonoidHom :=
  rfl
#align ring_equiv.to_add_monoid_hom_commutes RingEquiv.toAddMonoidMom_commutes

/-- The two paths coercion can take to a `MonoidHom` are equivalent -/
theorem toMonoidHom_commutes (f : R ‚âÉ+* S) :
    (f : R ‚Üí+* S).toMonoidHom = (f : R ‚âÉ* S).toMonoidHom :=
  rfl
#align ring_equiv.to_monoid_hom_commutes RingEquiv.toMonoidHom_commutes

/-- The two paths coercion can take to an `Equiv` are equivalent -/
theorem toEquiv_commutes (f : R ‚âÉ+* S) : (f : R ‚âÉ+ S).toEquiv = (f : R ‚âÉ* S).toEquiv :=
  rfl
#align ring_equiv.to_equiv_commutes RingEquiv.toEquiv_commutes

@[simp]
theorem toRingHom_refl : (RingEquiv.refl R).toRingHom = RingHom.id R :=
  rfl
#align ring_equiv.to_ring_hom_refl RingEquiv.toRingHom_refl

@[simp]
theorem toMonoidHom_refl : (RingEquiv.refl R).toMonoidHom = MonoidHom.id R :=
  rfl
#align ring_equiv.to_monoid_hom_refl RingEquiv.toMonoidHom_refl

@[simp]
theorem toAddMonoidHom_refl : (RingEquiv.refl R).toAddMonoidHom = AddMonoidHom.id R :=
  rfl
#align ring_equiv.to_add_monoid_hom_refl RingEquiv.toAddMonoidHom_refl

-- Porting note : Now other `simp` can do this, so removed `simp` attribute
theorem toRingHom_apply_symm_toRingHom_apply (e : R ‚âÉ+* S) :
    ‚àÄ y : S, e.toRingHom (e.symm.toRingHom y) = y :=
  e.toEquiv.apply_symm_apply
#align ring_equiv.to_ring_hom_apply_symm_to_ring_hom_apply RingEquiv.toRingHom_apply_symm_toRingHom_apply

-- Porting note : Now other `simp` can do this, so removed `simp` attribute
theorem symm_toRingHom_apply_toRingHom_apply (e : R ‚âÉ+* S) :
    ‚àÄ x : R, e.symm.toRingHom (e.toRingHom x) = x :=
  Equiv.symm_apply_apply e.toEquiv
#align ring_equiv.symm_to_ring_hom_apply_to_ring_hom_apply RingEquiv.symm_toRingHom_apply_toRingHom_apply

@[simp]
theorem toRingHom_trans (e‚ÇÅ : R ‚âÉ+* S) (e‚ÇÇ : S ‚âÉ+* S') :
    (e‚ÇÅ.trans e‚ÇÇ).toRingHom = e‚ÇÇ.toRingHom.comp e‚ÇÅ.toRingHom :=
  rfl
#align ring_equiv.to_ring_hom_trans RingEquiv.toRingHom_trans

-- Porting note : Now other `simp` can do this, so removed `simp` attribute
theorem toRingHom_comp_symm_toRingHom (e : R ‚âÉ+* S) :
    e.toRingHom.comp e.symm.toRingHom = RingHom.id _ := by
  ext
  -- ‚ä¢ ‚Üë(RingHom.comp (toRingHom e) (toRingHom (RingEquiv.symm e))) x‚úù = ‚Üë(RingHom. ‚Ä¶
  simp
  -- üéâ no goals
#align ring_equiv.to_ring_hom_comp_symm_to_ring_hom RingEquiv.toRingHom_comp_symm_toRingHom

-- Porting note : Now other `simp` can do this, so removed `simp` attribute
theorem symm_toRingHom_comp_toRingHom (e : R ‚âÉ+* S) :
    e.symm.toRingHom.comp e.toRingHom = RingHom.id _ := by
  ext
  -- ‚ä¢ ‚Üë(RingHom.comp (toRingHom (RingEquiv.symm e)) (toRingHom e)) x‚úù = ‚Üë(RingHom. ‚Ä¶
  simp
  -- üéâ no goals
#align ring_equiv.symm_to_ring_hom_comp_to_ring_hom RingEquiv.symm_toRingHom_comp_toRingHom

/-- Construct an equivalence of rings from homomorphisms in both directions, which are inverses.
-/
@[simps]
def ofHomInv' {R S F G : Type*} [NonUnitalNonAssocSemiring R] [NonUnitalNonAssocSemiring S]
    [NonUnitalRingHomClass F R S] [NonUnitalRingHomClass G S R] (hom : F) (inv : G)
    (hom_inv_id : (inv : S ‚Üí‚Çô+* R).comp (hom : R ‚Üí‚Çô+* S) = NonUnitalRingHom.id R)
    (inv_hom_id : (hom : R ‚Üí‚Çô+* S).comp (inv : S ‚Üí‚Çô+* R) = NonUnitalRingHom.id S) :
    R ‚âÉ+* S where
  toFun := hom
  invFun := inv
  left_inv := FunLike.congr_fun hom_inv_id
  right_inv := FunLike.congr_fun inv_hom_id
  map_mul' := map_mul hom
  map_add' := map_add hom
#align ring_equiv.of_hom_inv' RingEquiv.ofHomInv'
#align ring_equiv.of_hom_inv'_symm_apply RingEquiv.ofHomInv'_symm_apply
#align ring_equiv.of_hom_inv'_apply RingEquiv.ofHomInv'_apply

/--
Construct an equivalence of rings from unital homomorphisms in both directions, which are inverses.
-/
@[simps]
def ofHomInv {R S F G : Type*} [NonAssocSemiring R] [NonAssocSemiring S] [RingHomClass F R S]
    [RingHomClass G S R] (hom : F) (inv : G)
    (hom_inv_id : (inv : S ‚Üí+* R).comp (hom : R ‚Üí+* S) = RingHom.id R)
    (inv_hom_id : (hom : R ‚Üí+* S).comp (inv : S ‚Üí+* R) = RingHom.id S) :
    R ‚âÉ+* S where
  toFun := hom
  invFun := inv
  left_inv := FunLike.congr_fun hom_inv_id
  right_inv := FunLike.congr_fun inv_hom_id
  map_mul' := map_mul hom
  map_add' := map_add hom
#align ring_equiv.of_hom_inv RingEquiv.ofHomInv
#align ring_equiv.of_hom_inv_apply RingEquiv.ofHomInv_apply
#align ring_equiv.of_hom_inv_symm_apply RingEquiv.ofHomInv_symm_apply

end SemiringHom

section GroupPower

variable [Semiring R] [Semiring S]

protected theorem map_pow (f : R ‚âÉ+* S) (a) : ‚àÄ n : ‚Ñï, f (a ^ n) = f a ^ n :=
  map_pow f a
#align ring_equiv.map_pow RingEquiv.map_pow

end GroupPower

end RingEquiv

namespace MulEquiv

/-- Gives a `RingEquiv` from an element of a `MulEquivClass` preserving addition.-/
def toRingEquiv {R S F : Type*} [Add R] [Add S] [Mul R] [Mul S] [MulEquivClass F R S] (f : F)
    (H : ‚àÄ x y : R, f (x + y) = f x + f y) : R ‚âÉ+* S :=
  { (f : R ‚âÉ* S).toEquiv, (f : R ‚âÉ* S), AddEquiv.mk' (f : R ‚âÉ* S).toEquiv H with }
#align mul_equiv.to_ring_equiv MulEquiv.toRingEquiv

end MulEquiv

namespace AddEquiv

/-- Gives a `RingEquiv` from an element of an `AddEquivClass` preserving addition.-/
def toRingEquiv {R S F : Type*} [Add R] [Add S] [Mul R] [Mul S] [AddEquivClass F R S] (f : F)
    (H : ‚àÄ x y : R, f (x * y) = f x * f y) : R ‚âÉ+* S :=
  { (f : R ‚âÉ+ S).toEquiv, (f : R ‚âÉ+ S), MulEquiv.mk' (f : R ‚âÉ+ S).toEquiv H with }
#align add_equiv.to_ring_equiv AddEquiv.toRingEquiv

end AddEquiv

namespace RingEquiv

variable [Add R] [Add S] [Mul R] [Mul S]

@[simp]
theorem self_trans_symm (e : R ‚âÉ+* S) : e.trans e.symm = RingEquiv.refl R :=
  ext e.left_inv
#align ring_equiv.self_trans_symm RingEquiv.self_trans_symm

@[simp]
theorem symm_trans_self (e : R ‚âÉ+* S) : e.symm.trans e = RingEquiv.refl S :=
  ext e.right_inv
#align ring_equiv.symm_trans_self RingEquiv.symm_trans_self

end RingEquiv

namespace MulEquiv

/-- If two rings are isomorphic, and the second doesn't have zero divisors,
then so does the first. -/
protected theorem noZeroDivisors {A : Type*} (B : Type*) [MulZeroClass A] [MulZeroClass B]
    [NoZeroDivisors B] (e : A ‚âÉ* B) : NoZeroDivisors A :=
  e.injective.noZeroDivisors e (map_zero e) (map_mul e)
#noalign ring_equiv.no_zero_divisors

/-- If two rings are isomorphic, and the second is a domain, then so is the first. -/
protected theorem isDomain {A : Type*} (B : Type*) [Semiring A] [Semiring B] [IsDomain B]
    (e : A ‚âÉ* B) : IsDomain A :=
  { e.injective.isLeftCancelMulZero e (map_zero e) (map_mul e),
    e.injective.isRightCancelMulZero e (map_zero e) (map_mul e) with
    exists_pair_ne := ‚ü®e.symm 0, e.symm 1, e.symm.injective.ne zero_ne_one‚ü© }
#noalign ring_equiv.is_domain

protected theorem isField {A : Type*} (B : Type*) [Semiring A] [Semiring B] (hB : IsField B)
    (e : A ‚âÉ* B) : IsField A where
  exists_pair_ne := have ‚ü®x, y, h‚ü© := hB.exists_pair_ne; ‚ü®e.symm x, e.symm y, e.symm.injective.ne h‚ü©
  mul_comm := fun x y => e.injective <| by rw [map_mul, map_mul, hB.mul_comm]
                                           -- üéâ no goals
  mul_inv_cancel := fun h => by
    obtain ‚ü®a', he‚ü© := hB.mul_inv_cancel ((e.injective.ne h).trans_eq <| map_zero e)
    -- ‚ä¢ ‚àÉ b, a‚úù * b = 1
    exact ‚ü®e.symm a', e.injective <| by rw [map_mul, map_one, e.apply_symm_apply, he]‚ü©
    -- üéâ no goals

end MulEquiv

-- guard against import creep
assert_not_exists Fintype
