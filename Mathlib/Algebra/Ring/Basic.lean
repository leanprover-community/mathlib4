/-
Copyright (c) 2014 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Leonardo de Moura, Floris van Doorn, Yury Kudryashov, Neil Strickland
-/
import Mathlib.Algebra.Ring.Defs
import Mathlib.Algebra.Hom.Group
import Mathlib.Algebra.Opposites

#align_import algebra.ring.basic from "leanprover-community/mathlib"@"2ed7e4aec72395b6a7c3ac4ac7873a7a43ead17c"

/-!
# Semirings and rings

This file gives lemmas about semirings, rings and domains.
This is analogous to `Algebra.Group.Basic`,
the difference being that the former is about `+` and `*` separately, while
the present file is about their interaction.

For the definitions of semirings and rings see `Algebra.Ring.Defs`.
-/

set_option autoImplicit true

open Function

namespace AddHom

/-- Left multiplication by an element of a type with distributive multiplication is an `AddHom`. -/
@[simps (config := { fullyApplied := false })]
def mulLeft [Distrib R] (r : R) : AddHom R R :=
  ‚ü®(¬∑ * ¬∑) r, mul_add r‚ü©
#align add_hom.mul_left AddHom.mulLeft
#align add_hom.mul_left_apply AddHom.mulLeft_apply

/-- Left multiplication by an element of a type with distributive multiplication is an `AddHom`. -/
@[simps (config := { fullyApplied := false })]
def mulRight [Distrib R] (r : R) : AddHom R R :=
  ‚ü®fun a => a * r, fun _ _ => add_mul _ _ r‚ü©
#align add_hom.mul_right AddHom.mulRight
#align add_hom.mul_right_apply AddHom.mulRight_apply

end AddHom

section AddHomClass

variable {F : Type*} [NonAssocSemiring Œ±] [NonAssocSemiring Œ≤] [AddHomClass F Œ± Œ≤]

set_option linter.deprecated false in
/-- Additive homomorphisms preserve `bit0`. -/
@[deprecated, simp]
theorem map_bit0 (f : F) (a : Œ±) : (f (bit0 a) : Œ≤) = bit0 (f a) :=
  map_add _ _ _
#align map_bit0 map_bit0

end AddHomClass

namespace AddMonoidHom

/-- Left multiplication by an element of a (semi)ring is an `AddMonoidHom` -/
def mulLeft [NonUnitalNonAssocSemiring R] (r : R) : R ‚Üí+ R where
  toFun := (r * ¬∑)
  map_zero' := mul_zero r
  map_add' := mul_add r
#align add_monoid_hom.mul_left AddMonoidHom.mulLeft

@[simp]
theorem coe_mulLeft [NonUnitalNonAssocSemiring R] (r : R) :
    (mulLeft r : R ‚Üí R) = HMul.hMul r :=
  rfl
#align add_monoid_hom.coe_mul_left AddMonoidHom.coe_mulLeft

/-- Right multiplication by an element of a (semi)ring is an `AddMonoidHom` -/
def mulRight [NonUnitalNonAssocSemiring R] (r : R) : R ‚Üí+ R where
  toFun a := a * r
  map_zero' := zero_mul r
  map_add' _ _ := add_mul _ _ r
#align add_monoid_hom.mul_right AddMonoidHom.mulRight

@[simp]
theorem coe_mulRight [NonUnitalNonAssocSemiring R] (r : R) :
    (mulRight r) = (¬∑ * r) :=
  rfl
#align add_monoid_hom.coe_mul_right AddMonoidHom.coe_mulRight

theorem mulRight_apply [NonUnitalNonAssocSemiring R] (a r : R) :
    mulRight r a = a * r :=
  rfl
#align add_monoid_hom.mul_right_apply AddMonoidHom.mulRight_apply

end AddMonoidHom

section HasDistribNeg

section Mul

variable [Mul Œ±] [HasDistribNeg Œ±]

open MulOpposite

instance hasDistribNeg : HasDistribNeg Œ±·µê·µí·µñ :=
  { MulOpposite.involutiveNeg _ with
    neg_mul := fun _ _ => unop_injective <| mul_neg _ _,
    mul_neg := fun _ _ => unop_injective <| neg_mul _ _ }

end Mul

section Group

variable [Group Œ±] [HasDistribNeg Œ±]

@[simp]
theorem inv_neg' (a : Œ±) : (-a)‚Åª¬π = -a‚Åª¬π := by
  rw [eq_comm, eq_inv_iff_mul_eq_one, neg_mul, mul_neg, neg_neg, mul_left_inv]
  -- üéâ no goals
#align inv_neg' inv_neg'

end Group

end HasDistribNeg

section NonUnitalCommRing

variable [NonUnitalCommRing Œ±] {a b c : Œ±}

attribute [local simp] add_assoc add_comm add_left_comm mul_comm

/-- Vieta's formula for a quadratic equation, relating the coefficients of the polynomial with
  its roots. This particular version states that if we have a root `x` of a monic quadratic
  polynomial, then there is another root `y` such that `x + y` is negative the `a_1` coefficient
  and `x * y` is the `a_0` coefficient. -/
theorem vieta_formula_quadratic {b c x : Œ±} (h : x * x - b * x + c = 0) :
    ‚àÉ y : Œ±, y * y - b * y + c = 0 ‚àß x + y = b ‚àß x * y = c := by
  have : c = x * (b - x) := (eq_neg_of_add_eq_zero_right h).trans (by simp [mul_sub, mul_comm])
  -- ‚ä¢ ‚àÉ y, y * y - b * y + c = 0 ‚àß x + y = b ‚àß x * y = c
  refine' ‚ü®b - x, _, by simp, by rw [this]‚ü©
  -- ‚ä¢ (b - x) * (b - x) - b * (b - x) + c = 0
  rw [this, sub_add, ‚Üê sub_mul, sub_self]
  -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align Vieta_formula_quadratic vieta_formula_quadratic

end NonUnitalCommRing

theorem succ_ne_self [NonAssocRing Œ±] [Nontrivial Œ±] (a : Œ±) : a + 1 ‚â† a := fun h =>
  one_ne_zero ((add_right_inj a).mp (by simp [h]))
                                        -- üéâ no goals
#align succ_ne_self succ_ne_self

theorem pred_ne_self [NonAssocRing Œ±] [Nontrivial Œ±] (a : Œ±) : a - 1 ‚â† a := fun h ‚Ü¶
  one_ne_zero (neg_injective ((add_right_inj a).mp (by simp [‚Üêsub_eq_add_neg, h])))
                                                       -- üéâ no goals
#align pred_ne_self pred_ne_self

section NoZeroDivisors

variable (Œ±)

lemma IsLeftCancelMulZero.to_noZeroDivisors [Ring Œ±] [IsLeftCancelMulZero Œ±] :
    NoZeroDivisors Œ± :=
{ eq_zero_or_eq_zero_of_mul_eq_zero := @fun x y h ‚Ü¶ by
    by_cases hx : x = 0
    -- ‚ä¢ x = 0 ‚à® y = 0
    { left
      exact hx }
    { right
      rw [‚Üê sub_zero (x * y), ‚Üê mul_zero x, ‚Üê mul_sub] at h
      have := (IsLeftCancelMulZero.mul_left_cancel_of_ne_zero) hx h
      rwa [sub_zero] at this } }
#align is_left_cancel_mul_zero.to_no_zero_divisors IsLeftCancelMulZero.to_noZeroDivisors

lemma IsRightCancelMulZero.to_noZeroDivisors [Ring Œ±] [IsRightCancelMulZero Œ±] :
    NoZeroDivisors Œ± :=
{ eq_zero_or_eq_zero_of_mul_eq_zero := @fun x y h ‚Ü¶ by
    by_cases hy : y = 0
    -- ‚ä¢ x = 0 ‚à® y = 0
    { right
      exact hy }
    { left
      rw [‚Üê sub_zero (x * y), ‚Üê zero_mul y, ‚Üê sub_mul] at h
      have := (IsRightCancelMulZero.mul_right_cancel_of_ne_zero) hy h
      rwa [sub_zero] at this } }
#align is_right_cancel_mul_zero.to_no_zero_divisors IsRightCancelMulZero.to_noZeroDivisors

instance (priority := 100) NoZeroDivisors.to_isCancelMulZero [Ring Œ±] [NoZeroDivisors Œ±] :
    IsCancelMulZero Œ± :=
{ mul_left_cancel_of_ne_zero := fun ha h ‚Ü¶ by
    rw [‚Üê sub_eq_zero, ‚Üê mul_sub] at h
    -- ‚ä¢ b‚úù = c‚úù
    exact sub_eq_zero.1 ((eq_zero_or_eq_zero_of_mul_eq_zero h).resolve_left ha)
    -- üéâ no goals
  mul_right_cancel_of_ne_zero := fun hb h ‚Ü¶ by
    rw [‚Üê sub_eq_zero, ‚Üê sub_mul] at h
    -- ‚ä¢ a‚úù = c‚úù
    exact sub_eq_zero.1 ((eq_zero_or_eq_zero_of_mul_eq_zero h).resolve_right hb) }
    -- üéâ no goals
#align no_zero_divisors.to_is_cancel_mul_zero NoZeroDivisors.to_isCancelMulZero

lemma NoZeroDivisors.to_isDomain [Ring Œ±] [h : Nontrivial Œ±] [NoZeroDivisors Œ±] :
  IsDomain Œ± :=
{ NoZeroDivisors.to_isCancelMulZero Œ±, h with .. }
#align no_zero_divisors.to_is_domain NoZeroDivisors.to_isDomain

instance (priority := 100) IsDomain.to_noZeroDivisors [Ring Œ±] [IsDomain Œ±] :
    NoZeroDivisors Œ± :=
IsRightCancelMulZero.to_noZeroDivisors Œ±
#align is_domain.to_no_zero_divisors IsDomain.to_noZeroDivisors

end NoZeroDivisors
