/-
Copyright (c) 2024 Amelia Livingston. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Amelia Livingston
-/
import Mathlib.Algebra.Category.ModuleCat.Basic
import Mathlib.RingTheory.Coalgebra.Equiv

/-!
# The category of coalgebras over a commutative ring

We introduce the bundled category `Coalg` of coalgebras over a fixed commutative ring `R`
along with the forgetful functor to `ModuleCat`.

This file mimics `Mathlib.LinearAlgebra.QuadraticForm.QuadraticModuleCat`.

-/

open CategoryTheory

universe v u

variable (R : Type u) [CommRing R]

/-- The category of `R`-coalgebras. -/
structure Coalg extends ModuleCat.{v} R where
  instCoalgebra : Coalgebra R carrier

attribute [instance] Coalg.instCoalgebra

variable {R}

namespace Coalg

open Coalgebra

instance : CoeSort (Coalg.{v} R) (Type v) :=
  âŸ¨(Â·.carrier)âŸ©

@[simp] theorem moduleCat_of_toModuleCat (X : Coalg.{v} R) :
    ModuleCat.of R X.toModuleCat = X.toModuleCat :=
  rfl

variable (R) in
/-- The object in the category of `R`-coalgebras associated to an `R`-coalgebra. -/
abbrev of (X : Type v) [AddCommGroup X] [Module R X] [Coalgebra R X] :
    Coalg R :=
  { ModuleCat.of R X with
    instCoalgebra := (inferInstance : Coalgebra R X) }

@[simp]
lemma of_comul {X : Type v} [AddCommGroup X] [Module R X] [Coalgebra R X] :
    Coalgebra.comul (A := of R X) = Coalgebra.comul (R := R) (A := X) := rfl

@[simp]
lemma of_counit {X : Type v} [AddCommGroup X] [Module R X] [Coalgebra R X] :
    Coalgebra.counit (A := of R X) = Coalgebra.counit (R := R) (A := X) := rfl

/-- A type alias for `CoalgHom` to avoid confusion between the categorical and
algebraic spellings of composition. -/
@[ext]
structure Hom (V W : Coalg.{v} R) where
  /-- The underlying `CoalgHom` -/
  toCoalgHom' : V â†’â‚—c[R] W

instance category : Category (Coalg.{v} R) where
  Hom M N := Hom M N
  id M := âŸ¨CoalgHom.id R MâŸ©
  comp f g := âŸ¨CoalgHom.comp g.toCoalgHom' f.toCoalgHom'âŸ©

instance concreteCategory : ConcreteCategory (Coalg.{v} R) (Â· â†’â‚—c[R] Â·) where
  hom f := f.toCoalgHom'
  ofHom f := âŸ¨fâŸ©

/-- Turn a morphism in `Coalg` back into a `CoalgHom`. -/
abbrev Hom.toCoalgHom {X Y : Coalg.{v} R} (f : Hom X Y) : X â†’â‚—c[R] Y :=
  ConcreteCategory.hom (C := Coalg.{v} R) f

/-- Typecheck a `CoalgHom` as a morphism in `Coalg R`. -/
abbrev ofHom {X Y : Type v} [AddCommGroup X] [Module R X] [AddCommGroup Y] [Module R Y]
    [Coalgebra R X] [Coalgebra R Y] (f : X â†’â‚—c[R] Y) :
    of R X âŸ¶ of R Y :=
  ConcreteCategory.ofHom f

lemma Hom.toCoalgHom_injective (V W : Coalg.{v} R) :
    Function.Injective (Hom.toCoalgHom' : Hom V W â†’ _) :=
  fun âŸ¨fâŸ© âŸ¨gâŸ© _ => by congr

@[ext]
lemma hom_ext {M N : Coalg.{v} R} (f g : M âŸ¶ N) (h : f.toCoalgHom = g.toCoalgHom) :
    f = g :=
  Hom.ext h

@[simp] theorem toCoalgHom_comp {M N U : Coalg.{v} R} (f : M âŸ¶ N) (g : N âŸ¶ U) :
    (f â‰« g).toCoalgHom = g.toCoalgHom.comp f.toCoalgHom :=
  rfl

@[simp] theorem toCoalgHom_id {M : Coalg.{v} R} :
    Hom.toCoalgHom (ðŸ™ M) = CoalgHom.id _ _ :=
  rfl

instance hasForgetToModule : HasForgetâ‚‚ (Coalg R) (ModuleCat R) where
  forgetâ‚‚ :=
    { obj := fun M => ModuleCat.of R M
      map := fun f => ModuleCat.ofHom f.toCoalgHom.toLinearMap }

@[simp]
theorem forgetâ‚‚_obj (X : Coalg R) :
    (forgetâ‚‚ (Coalg R) (ModuleCat R)).obj X = ModuleCat.of R X :=
  rfl

@[simp]
theorem forgetâ‚‚_map (X Y : Coalg R) (f : X âŸ¶ Y) :
    (forgetâ‚‚ (Coalg R) (ModuleCat R)).map f = ModuleCat.ofHom (f.toCoalgHom : X â†’â‚—[R] Y) :=
  rfl

end Coalg

namespace CoalgEquiv

open Coalg

variable {X Y Z : Type v}
variable [AddCommGroup X] [Module R X] [AddCommGroup Y] [Module R Y] [AddCommGroup Z] [Module R Z]
variable [Coalgebra R X] [Coalgebra R Y] [Coalgebra R Z]

/-- Build an isomorphism in the category `Coalg R` from a
`CoalgEquiv`. -/
@[simps]
def toCoalgIso (e : X â‰ƒâ‚—c[R] Y) : Coalg.of R X â‰… Coalg.of R Y where
  hom := Coalg.ofHom e
  inv := Coalg.ofHom e.symm
  hom_inv_id := Hom.ext <| DFunLike.ext _ _ e.left_inv
  inv_hom_id := Hom.ext <| DFunLike.ext _ _ e.right_inv

@[simp] theorem toCoalgIso_refl :
    toCoalgIso (CoalgEquiv.refl R X) = .refl _ :=
  rfl

@[simp] theorem toCoalgIso_symm (e : X â‰ƒâ‚—c[R] Y) :
    toCoalgIso e.symm = (toCoalgIso e).symm :=
  rfl

@[simp] theorem toCoalgIso_trans (e : X â‰ƒâ‚—c[R] Y) (f : Y â‰ƒâ‚—c[R] Z) :
    toCoalgIso (e.trans f) = toCoalgIso e â‰ªâ‰« toCoalgIso f :=
  rfl

end CoalgEquiv

namespace CategoryTheory.Iso

open Coalgebra

variable {X Y Z : Coalg.{v} R}

/-- Build a `CoalgEquiv` from an isomorphism in the category
`Coalg R`. -/
def toCoalgEquiv (i : X â‰… Y) : X â‰ƒâ‚—c[R] Y :=
  { i.hom.toCoalgHom with
    invFun := i.inv.toCoalgHom
    left_inv := fun x => CoalgHom.congr_fun (congr_arg Coalg.Hom.toCoalgHom i.3) x
    right_inv := fun x => CoalgHom.congr_fun (congr_arg Coalg.Hom.toCoalgHom i.4) x }

@[simp] theorem toCoalgEquiv_toCoalgHom (i : X â‰… Y) :
    i.toCoalgEquiv = i.hom.toCoalgHom := rfl

@[simp] theorem toCoalgEquiv_refl : toCoalgEquiv (.refl X) = .refl _ _ :=
  rfl

@[simp] theorem toCoalgEquiv_symm (e : X â‰… Y) :
    toCoalgEquiv e.symm = (toCoalgEquiv e).symm :=
  rfl

@[simp] theorem toCoalgEquiv_trans (e : X â‰… Y) (f : Y â‰… Z) :
    toCoalgEquiv (e â‰ªâ‰« f) = e.toCoalgEquiv.trans f.toCoalgEquiv :=
  rfl

end CategoryTheory.Iso

instance Coalg.forget_reflects_isos :
    (forget (Coalg.{v} R)).ReflectsIsomorphisms where
  reflects {X Y} f _ := by
    let i := asIso ((forget (Coalg.{v} R)).map f)
    let e : X â‰ƒâ‚—c[R] Y := { f.toCoalgHom, i.toEquiv with }
    exact âŸ¨e.toCoalgIso.isIso_hom.1âŸ©
