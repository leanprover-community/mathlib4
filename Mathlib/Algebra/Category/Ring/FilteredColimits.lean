/-
Copyright (c) 2021 Justus Springer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Justus Springer
-/
import Mathlib.Algebra.Category.Ring.Basic
import Mathlib.Algebra.Category.Grp.FilteredColimits
import Mathlib.Algebra.Ring.ULift

/-!
# The forgetful functor from (commutative) (semi-) rings preserves filtered colimits.

Forgetful functors from algebraic categories usually don't preserve colimits. However, they tend
to preserve _filtered_ colimits.

In this file, we start with a small filtered category `J` and a functor `F : J â¥¤ SemiRingCat`.
We show that the colimit of `F â‹™ forgetâ‚‚ SemiRingCat MonCat` (in `MonCat`)
carries the structure of a semiring, thereby showing that the forgetful functor
`forgetâ‚‚ SemiRingCat MonCat` preserves filtered colimits.
In particular, this implies that `forget SemiRingCat` preserves filtered colimits.
Similarly for `CommSemiRingCat`, `RingCat` and `CommRingCat`.

-/


universe v u

noncomputable section

open CategoryTheory Limits

open IsFiltered renaming max â†’ max' -- avoid name collision with `_root_.max`.

open AddMonCat.FilteredColimits (colimit_zero_eq colimit_add_mk_eq)

open MonCat.FilteredColimits (colimit_one_eq colimit_mul_mk_eq)

namespace SemiRingCat.FilteredColimits

section

-- We use parameters here, mainly so we can have the abbreviations `R` and `R.mk` below, without
-- passing around `F` all the time.
variable {J : Type v} [SmallCategory J] (F : J â¥¤ SemiRingCat.{max v u})

-- This instance is needed below in `colimitSemiring`, during the verification of the
-- semiring axioms.
instance semiringObj (j : J) :
    Semiring (((F â‹™ forgetâ‚‚ SemiRingCat.{max v u} MonCat) â‹™ forget MonCat).obj j) :=
  show Semiring (F.obj j) by infer_instance

variable [IsFiltered J]

/-- The colimit of `F â‹™ forgetâ‚‚ SemiRingCat MonCat` in the category `MonCat`.
In the following, we will show that this has the structure of a semiring.
-/
abbrev R : MonCat.{max v u} :=
  MonCat.FilteredColimits.colimit.{v, u} (F â‹™ forgetâ‚‚ SemiRingCat.{max v u} MonCat)

instance colimitSemiring : Semiring.{max v u} <| R.{v, u} F :=
  { (R.{v, u} F).str,
    AddCommMonCat.FilteredColimits.colimitAddCommMonoid.{v, u}
      (F â‹™ forgetâ‚‚ SemiRingCat AddCommMonCat.{max v u}) with
    mul_zero := fun x â†¦ by
      refine Quot.inductionOn x ?_; clear x; intro x
      obtain âŸ¨j, xâŸ© := x
      erw [colimit_zero_eq _ j, colimit_mul_mk_eq _ âŸ¨j, _âŸ© âŸ¨j, _âŸ© j (ðŸ™ j) (ðŸ™ j)]
      rw [CategoryTheory.Functor.map_id]
      dsimp
      rw [mul_zero x]
      rfl
    zero_mul := fun x â†¦ by
      refine Quot.inductionOn x ?_; clear x; intro x
      obtain âŸ¨j, xâŸ© := x
      erw [colimit_zero_eq _ j, colimit_mul_mk_eq _ âŸ¨j, _âŸ© âŸ¨j, _âŸ© j (ðŸ™ j) (ðŸ™ j)]
      rw [CategoryTheory.Functor.map_id]
      dsimp
      rw [zero_mul x]
      rfl
    left_distrib := fun x y z â†¦ by
      refine Quot.induction_onâ‚ƒ x y z ?_; clear x y z; intro x y z
      obtain âŸ¨jâ‚, xâŸ© := x; obtain âŸ¨jâ‚‚, yâŸ© := y; obtain âŸ¨jâ‚ƒ, zâŸ© := z
      let k := IsFiltered.maxâ‚ƒ jâ‚ jâ‚‚ jâ‚ƒ
      let f := IsFiltered.firstToMaxâ‚ƒ jâ‚ jâ‚‚ jâ‚ƒ
      let g := IsFiltered.secondToMaxâ‚ƒ jâ‚ jâ‚‚ jâ‚ƒ
      let h := IsFiltered.thirdToMaxâ‚ƒ jâ‚ jâ‚‚ jâ‚ƒ
      erw [colimit_add_mk_eq _ âŸ¨jâ‚‚, _âŸ© âŸ¨jâ‚ƒ, _âŸ© k g h, colimit_mul_mk_eq _ âŸ¨jâ‚, _âŸ© âŸ¨k, _âŸ© k f (ðŸ™ k),
        colimit_mul_mk_eq _ âŸ¨jâ‚, _âŸ© âŸ¨jâ‚‚, _âŸ© k f g, colimit_mul_mk_eq _ âŸ¨jâ‚, _âŸ© âŸ¨jâ‚ƒ, _âŸ© k f h,
        colimit_add_mk_eq _ âŸ¨k, _âŸ© âŸ¨k, _âŸ© k (ðŸ™ k) (ðŸ™ k)]
      simp only [CategoryTheory.Functor.map_id]
      erw [left_distrib (F.map f x) (F.map g y) (F.map h z)]
      rfl
    right_distrib := fun x y z â†¦ by
      refine Quot.induction_onâ‚ƒ x y z ?_; clear x y z; intro x y z
      obtain âŸ¨jâ‚, xâŸ© := x; obtain âŸ¨jâ‚‚, yâŸ© := y; obtain âŸ¨jâ‚ƒ, zâŸ© := z
      let k := IsFiltered.maxâ‚ƒ jâ‚ jâ‚‚ jâ‚ƒ
      let f := IsFiltered.firstToMaxâ‚ƒ jâ‚ jâ‚‚ jâ‚ƒ
      let g := IsFiltered.secondToMaxâ‚ƒ jâ‚ jâ‚‚ jâ‚ƒ
      let h := IsFiltered.thirdToMaxâ‚ƒ jâ‚ jâ‚‚ jâ‚ƒ
      erw [colimit_add_mk_eq _ âŸ¨jâ‚, _âŸ© âŸ¨jâ‚‚, _âŸ© k f g, colimit_mul_mk_eq _ âŸ¨k, _âŸ© âŸ¨jâ‚ƒ, _âŸ© k (ðŸ™ k) h,
        colimit_mul_mk_eq _ âŸ¨jâ‚, _âŸ© âŸ¨jâ‚ƒ, _âŸ© k f h, colimit_mul_mk_eq _ âŸ¨jâ‚‚, _âŸ© âŸ¨jâ‚ƒ, _âŸ© k g h,
        colimit_add_mk_eq _ âŸ¨k, _âŸ© âŸ¨k, _âŸ© k (ðŸ™ k) (ðŸ™ k)]
      simp only [CategoryTheory.Functor.map_id]
      erw [right_distrib (F.map f x) (F.map g y) (F.map h z)]
      rfl }

/-- The bundled semiring giving the filtered colimit of a diagram. -/
def colimit : SemiRingCat.{max v u} :=
  SemiRingCat.of <| R.{v, u} F

/-- The cocone over the proposed colimit semiring. -/
def colimitCocone : Cocone F where
  pt := colimit.{v, u} F
  Î¹ :=
    { app := fun j â†¦ ofHom
        { ((MonCat.FilteredColimits.colimitCocone
            (F â‹™ forgetâ‚‚ SemiRingCat.{max v u} MonCat)).Î¹.app j).hom,
            ((AddCommMonCat.FilteredColimits.colimitCocone
              (F â‹™ forgetâ‚‚ SemiRingCat.{max v u} AddCommMonCat)).Î¹.app j).hom with }
      naturality := fun {_ _} f â†¦ hom_ext <|
        RingHom.coe_inj ((Types.TypeMax.colimitCocone (F â‹™ forget SemiRingCat)).Î¹.naturality f) }

namespace colimitCoconeIsColimit

variable {F} (t : Cocone F)

/-- Auxiliary definition for `colimitCoconeIsColimit`. -/
def descAddMonoidHom : R F â†’+ t.1 :=
  ((AddCommMonCat.FilteredColimits.colimitCoconeIsColimit.{v, u}
    (F â‹™ forgetâ‚‚ SemiRingCat AddCommMonCat)).desc
      ((forgetâ‚‚ SemiRingCat AddCommMonCat).mapCocone t)).hom

lemma descAddMonoidHom_quotMk {j : J} (x : F.obj j) :
    descAddMonoidHom t (Quot.mk _ âŸ¨j, xâŸ©) = t.Î¹.app j x :=
  congr_fun ((forget _).congr_map
    ((AddCommMonCat.FilteredColimits.colimitCoconeIsColimit.{v, u}
      (F â‹™ forgetâ‚‚ SemiRingCat AddCommMonCat)).fac
        ((forgetâ‚‚ SemiRingCat AddCommMonCat).mapCocone t) j)) x

/-- Auxiliary definition for `colimitCoconeIsColimit`. -/
def descMonoidHom : R F â†’* t.1 :=
  ((MonCat.FilteredColimits.colimitCoconeIsColimit.{v, u}
    (F â‹™ forgetâ‚‚ _ _)).desc ((forgetâ‚‚ _ _).mapCocone t)).hom

lemma descMonoidHom_quotMk {j : J} (x : F.obj j) :
    descMonoidHom t (Quot.mk _ âŸ¨j, xâŸ©) = t.Î¹.app j x :=
  congr_fun ((forget _).congr_map
    ((MonCat.FilteredColimits.colimitCoconeIsColimit.{v, u}
      (F â‹™ forgetâ‚‚ _ _)).fac ((forgetâ‚‚ _ _).mapCocone t) j)) x

lemma descMonoidHom_apply_eq (x : R F) :
    descMonoidHom t x = descAddMonoidHom t x := by
  obtain âŸ¨j, xâŸ© := x
  rw [descMonoidHom_quotMk t x, descAddMonoidHom_quotMk t x]

end colimitCoconeIsColimit

open colimitCoconeIsColimit in
/-- The proposed colimit cocone is a colimit in `SemiRingCat`. -/
def colimitCoconeIsColimit : IsColimit <| colimitCocone.{v, u} F where
  desc t := ofHom
    { descAddMonoidHom t with
      map_one' := (descMonoidHom_apply_eq t 1).symm.trans (by simp)
      map_mul' x y := by
        change descAddMonoidHom t (x * y) =
          descAddMonoidHom t x * descAddMonoidHom t y
        simp [â† descMonoidHom_apply_eq] }
  fac t j := by ext x; exact descAddMonoidHom_quotMk t x
  uniq t m hm := by
    ext âŸ¨j, xâŸ©
    exact (congr_fun ((forget _).congr_map (hm j)) x).trans
      (descAddMonoidHom_quotMk t x).symm

instance forgetâ‚‚Mon_preservesFilteredColimits :
    PreservesFilteredColimits (forgetâ‚‚ SemiRingCat MonCat.{u}) where
  preserves_filtered_colimits {J hJ1 _} :=
    letI : Category J := hJ1
    { preservesColimit := fun {F} â†¦
        preservesColimit_of_preserves_colimit_cocone (colimitCoconeIsColimit.{u, u} F)
          (MonCat.FilteredColimits.colimitCoconeIsColimit (F â‹™ forgetâ‚‚ SemiRingCat MonCat.{u})) }

instance forget_preservesFilteredColimits : PreservesFilteredColimits (forget SemiRingCat.{u}) :=
  Limits.comp_preservesFilteredColimits (forgetâ‚‚ SemiRingCat MonCat) (forget MonCat.{u})

end

end SemiRingCat.FilteredColimits

namespace CommSemiRingCat.FilteredColimits

section

-- We use parameters here, mainly so we can have the abbreviation `R` below, without
-- passing around `F` all the time.
variable {J : Type v} [SmallCategory J] [IsFiltered J] (F : J â¥¤ CommSemiRingCat.{max v u})

/-- The colimit of `F â‹™ forgetâ‚‚ CommSemiRingCat SemiRingCat` in the category `SemiRingCat`.
In the following, we will show that this has the structure of a _commutative_ semiring.
-/
abbrev R : SemiRingCat.{max v u} :=
  SemiRingCat.FilteredColimits.colimit (F â‹™ forgetâ‚‚ CommSemiRingCat SemiRingCat.{max v u})

instance colimitCommSemiring : CommSemiring.{max v u} <| R.{v, u} F :=
  { (R F).semiring,
    CommMonCat.FilteredColimits.colimitCommMonoid
      (F â‹™ forgetâ‚‚ CommSemiRingCat CommMonCat.{max v u}) with }

/-- The bundled commutative semiring giving the filtered colimit of a diagram. -/
def colimit : CommSemiRingCat.{max v u} :=
  CommSemiRingCat.of <| R.{v, u} F

/-- The cocone over the proposed colimit commutative semiring. -/
def colimitCocone : Cocone F where
  pt := colimit.{v, u} F
  Î¹ :=
    { app := fun X â†¦ ofHom <| ((SemiRingCat.FilteredColimits.colimitCocone
          (F â‹™ forgetâ‚‚ CommSemiRingCat SemiRingCat.{max v u})).Î¹.app X).hom
      naturality := fun _ _ f â†¦ hom_ext <|
        RingHom.coe_inj ((Types.TypeMax.colimitCocone
          (F â‹™ forget CommSemiRingCat)).Î¹.naturality f) }


/-- The proposed colimit cocone is a colimit in `CommSemiRingCat`. -/
def colimitCoconeIsColimit : IsColimit <| colimitCocone.{v, u} F :=
  isColimitOfReflects (forgetâ‚‚ _ SemiRingCat)
    (SemiRingCat.FilteredColimits.colimitCoconeIsColimit
      (F â‹™ forgetâ‚‚ CommSemiRingCat SemiRingCat))

instance forgetâ‚‚SemiRing_preservesFilteredColimits :
    PreservesFilteredColimits (forgetâ‚‚ CommSemiRingCat SemiRingCat.{u}) where
  preserves_filtered_colimits {J hJ1 _} :=
    letI : Category J := hJ1
    { preservesColimit := fun {F} â†¦
        preservesColimit_of_preserves_colimit_cocone (colimitCoconeIsColimit.{u, u} F)
          (SemiRingCat.FilteredColimits.colimitCoconeIsColimit
            (F â‹™ forgetâ‚‚ CommSemiRingCat SemiRingCat.{u})) }

instance forget_preservesFilteredColimits :
    PreservesFilteredColimits (forget CommSemiRingCat.{u}) :=
  Limits.comp_preservesFilteredColimits (forgetâ‚‚ CommSemiRingCat SemiRingCat)
    (forget SemiRingCat.{u})

end

end CommSemiRingCat.FilteredColimits

namespace RingCat.FilteredColimits

section

-- We use parameters here, mainly so we can have the abbreviation `R` below, without
-- passing around `F` all the time.
variable {J : Type v} [SmallCategory J] [IsFiltered J] (F : J â¥¤ RingCat.{max v u})

/-- The colimit of `F â‹™ forgetâ‚‚ RingCat SemiRingCat` in the category `SemiRingCat`.
In the following, we will show that this has the structure of a ring.
-/
abbrev R : SemiRingCat.{max v u} :=
  SemiRingCat.FilteredColimits.colimit.{v, u} (F â‹™ forgetâ‚‚ RingCat SemiRingCat.{max v u})

instance colimitRing : Ring.{max v u} <| R.{v, u} F :=
  { (R F).semiring,
    AddCommGrp.FilteredColimits.colimitAddCommGroup.{v, u}
      (F â‹™ forgetâ‚‚ RingCat AddCommGrp.{max v u}) with }

/-- The bundled ring giving the filtered colimit of a diagram. -/
def colimit : RingCat.{max v u} :=
  RingCat.of <| R.{v, u} F

/-- The cocone over the proposed colimit ring. -/
def colimitCocone : Cocone F where
  pt := colimit.{v, u} F
  Î¹ :=
    { app := fun X â†¦ ofHom <| ((SemiRingCat.FilteredColimits.colimitCocone
          (F â‹™ forgetâ‚‚ RingCat SemiRingCat.{max v u})).Î¹.app X).hom
      naturality := fun _ _ f â†¦ hom_ext <|
        RingHom.coe_inj ((Types.TypeMax.colimitCocone (F â‹™ forget RingCat)).Î¹.naturality f) }

/-- The proposed colimit cocone is a colimit in `Ring`. -/
def colimitCoconeIsColimit : IsColimit <| colimitCocone.{v, u} F :=
  isColimitOfReflects (forgetâ‚‚ _ _)
    (SemiRingCat.FilteredColimits.colimitCoconeIsColimit
      (F â‹™ forgetâ‚‚ RingCat SemiRingCat))

instance forgetâ‚‚SemiRing_preservesFilteredColimits :
    PreservesFilteredColimits (forgetâ‚‚ RingCat SemiRingCat.{u}) where
  preserves_filtered_colimits {J hJ1 _} :=
    letI : Category J := hJ1
    { preservesColimit := fun {F} â†¦
        preservesColimit_of_preserves_colimit_cocone (colimitCoconeIsColimit.{u, u} F)
          (SemiRingCat.FilteredColimits.colimitCoconeIsColimit
            (F â‹™ forgetâ‚‚ RingCat SemiRingCat.{u})) }

instance forget_preservesFilteredColimits : PreservesFilteredColimits (forget RingCat.{u}) :=
  Limits.comp_preservesFilteredColimits (forgetâ‚‚ RingCat SemiRingCat) (forget SemiRingCat.{u})

end

end RingCat.FilteredColimits

namespace CommRingCat.FilteredColimits

section

-- We use parameters here, mainly so we can have the abbreviation `R` below, without
-- passing around `F` all the time.
variable {J : Type v} [SmallCategory J] [IsFiltered J] (F : J â¥¤ CommRingCat.{max v u})

/-- The colimit of `F â‹™ forgetâ‚‚ CommRingCat RingCat` in the category `RingCat`.
In the following, we will show that this has the structure of a _commutative_ ring.
-/
abbrev R : RingCat.{max v u} :=
  RingCat.FilteredColimits.colimit.{v, u} (F â‹™ forgetâ‚‚ CommRingCat RingCat.{max v u})

instance colimitCommRing : CommRing.{max v u} <| R.{v, u} F :=
  { (R.{v, u} F).ring,
    CommSemiRingCat.FilteredColimits.colimitCommSemiring
      (F â‹™ forgetâ‚‚ CommRingCat CommSemiRingCat.{max v u}) with }

/-- The bundled commutative ring giving the filtered colimit of a diagram. -/
def colimit : CommRingCat.{max v u} :=
  CommRingCat.of <| R.{v, u} F

/-- The cocone over the proposed colimit commutative ring. -/
def colimitCocone : Cocone F where
  pt := colimit.{v, u} F
  Î¹ :=
    { app := fun X â†¦ ofHom <| ((RingCat.FilteredColimits.colimitCocone
          (F â‹™ forgetâ‚‚ CommRingCat RingCat.{max v u})).Î¹.app X).hom
      naturality := fun _ _ f â†¦ hom_ext <|
        RingHom.coe_inj ((Types.TypeMax.colimitCocone (F â‹™ forget CommRingCat)).Î¹.naturality f) }

/-- The proposed colimit cocone is a colimit in `CommRingCat`. -/
def colimitCoconeIsColimit : IsColimit <| colimitCocone.{v, u} F :=
  isColimitOfReflects (forgetâ‚‚ _ _)
    (RingCat.FilteredColimits.colimitCoconeIsColimit
      (F â‹™ forgetâ‚‚ CommRingCat RingCat))

instance forgetâ‚‚Ring_preservesFilteredColimits :
    PreservesFilteredColimits (forgetâ‚‚ CommRingCat RingCat.{u}) where
  preserves_filtered_colimits {J hJ1 _} :=
    letI : Category J := hJ1
    { preservesColimit := fun {F} â†¦
        preservesColimit_of_preserves_colimit_cocone (colimitCoconeIsColimit.{u, u} F)
          (RingCat.FilteredColimits.colimitCoconeIsColimit (F â‹™ forgetâ‚‚ CommRingCat RingCat.{u})) }

instance forget_preservesFilteredColimits : PreservesFilteredColimits (forget CommRingCat.{u}) :=
  Limits.comp_preservesFilteredColimits (forgetâ‚‚ CommRingCat RingCat) (forget RingCat.{u})

omit [IsFiltered J] in
protected lemma nontrivial {F : J â¥¤ CommRingCat.{v}} [IsFilteredOrEmpty J]
    [âˆ€ i, Nontrivial (F.obj i)] {c : Cocone F} (hc : IsColimit c) : Nontrivial c.pt := by
  classical
  cases isEmpty_or_nonempty J
  Â· exact ((isColimitEquivIsInitialOfIsEmpty _ _ hc).to (.of (ULift â„¤))).hom.domain_nontrivial
  have i := â€¹Nonempty Jâ€º.some
  refine âŸ¨c.Î¹.app i 0, c.Î¹.app i 1, fun h â†¦ ?_âŸ©
  have : IsFiltered J := âŸ¨âŸ©
  obtain âŸ¨k, f, eâŸ© :=
    (Types.FilteredColimit.isColimit_eq_iff' (isColimitOfPreserves (forget _) hc) _ _).mp h
  exact zero_ne_one (((F.map f).hom.map_zero.symm.trans e).trans (F.map f).hom.map_one)

omit [IsFiltered J] in
instance {F : J â¥¤ CommRingCat.{v}} [IsFilteredOrEmpty J]
    [HasColimit F] [âˆ€ i, Nontrivial (F.obj i)] : Nontrivial â†‘(Limits.colimit F) :=
  FilteredColimits.nontrivial (getColimitCocone F).2

end

end CommRingCat.FilteredColimits
