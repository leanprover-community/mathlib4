/-
Copyright (c) 2021 Justus Springer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Justus Springer
-/
import Mathlib.Algebra.Category.Ring.Basic
import Mathlib.Algebra.Category.Grp.FilteredColimits
import Mathlib.Algebra.Ring.ULift

/-!
# The forgetful functor from (commutative) (semi-) rings preserves filtered colimits.

Forgetful functors from algebraic categories usually don't preserve colimits. However, they tend
to preserve _filtered_ colimits.

In this file, we start with a small filtered category `J` and a functor `F : J ‚•§ SemiRingCat`.
We show that the colimit of `F ‚ãô forget‚ÇÇ SemiRingCat MonCat` (in `MonCat`)
carries the structure of a semiring, thereby showing that the forgetful functor
`forget‚ÇÇ SemiRingCat MonCat` preserves filtered colimits.
In particular, this implies that `forget SemiRingCat` preserves filtered colimits.
Similarly for `CommSemiRingCat`, `RingCat` and `CommRingCat`.

-/


universe v u

noncomputable section

open CategoryTheory Limits

open IsFiltered renaming max ‚Üí max' -- avoid name collision with `_root_.max`.

open AddMonCat.FilteredColimits (colimit_zero_eq colimit_add_mk_eq)

open MonCat.FilteredColimits (colimit_one_eq colimit_mul_mk_eq)

namespace SemiRingCat.FilteredColimits

section

-- We use parameters here, mainly so we can have the abbreviations `R` and `R.mk` below, without
-- passing around `F` all the time.
variable {J : Type v} [SmallCategory J] (F : J ‚•§ SemiRingCat.{max v u})

-- This instance is needed below in `colimitSemiring`, during the verification of the
-- semiring axioms.
instance semiringObj (j : J) :
    Semiring (((F ‚ãô forget‚ÇÇ SemiRingCat.{max v u} MonCat) ‚ãô forget MonCat).obj j) :=
  show Semiring (F.obj j) by infer_instance

variable [IsFiltered J]

/-- The colimit of `F ‚ãô forget‚ÇÇ SemiRingCat MonCat` in the category `MonCat`.
In the following, we will show that this has the structure of a semiring.
-/
abbrev R : MonCat.{max v u} :=
  MonCat.FilteredColimits.colimit.{v, u} (F ‚ãô forget‚ÇÇ SemiRingCat.{max v u} MonCat)

instance colimitSemiring : Semiring.{max v u} <| R.{v, u} F :=
  { (R.{v, u} F).str,
    AddCommMonCat.FilteredColimits.colimitAddCommMonoid.{v, u}
      (F ‚ãô forget‚ÇÇ SemiRingCat AddCommMonCat.{max v u}) with
    mul_zero := fun x => by
      refine Quot.inductionOn x ?_; clear x; intro x
      obtain ‚ü®j, x‚ü© := x
      erw [colimit_zero_eq _ j, colimit_mul_mk_eq _ ‚ü®j, _‚ü© ‚ü®j, _‚ü© j (ùüô j) (ùüô j)]
      rw [CategoryTheory.Functor.map_id]
      dsimp
      rw [mul_zero x]
      rfl
    zero_mul := fun x => by
      refine Quot.inductionOn x ?_; clear x; intro x
      obtain ‚ü®j, x‚ü© := x
      erw [colimit_zero_eq _ j, colimit_mul_mk_eq _ ‚ü®j, _‚ü© ‚ü®j, _‚ü© j (ùüô j) (ùüô j)]
      rw [CategoryTheory.Functor.map_id]
      dsimp
      rw [zero_mul x]
      rfl
    left_distrib := fun x y z => by
      refine Quot.induction_on‚ÇÉ x y z ?_; clear x y z; intro x y z
      obtain ‚ü®j‚ÇÅ, x‚ü© := x; obtain ‚ü®j‚ÇÇ, y‚ü© := y; obtain ‚ü®j‚ÇÉ, z‚ü© := z
      let k := IsFiltered.max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ
      let f := IsFiltered.firstToMax‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ
      let g := IsFiltered.secondToMax‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ
      let h := IsFiltered.thirdToMax‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ
      erw [colimit_add_mk_eq _ ‚ü®j‚ÇÇ, _‚ü© ‚ü®j‚ÇÉ, _‚ü© k g h, colimit_mul_mk_eq _ ‚ü®j‚ÇÅ, _‚ü© ‚ü®k, _‚ü© k f (ùüô k),
        colimit_mul_mk_eq _ ‚ü®j‚ÇÅ, _‚ü© ‚ü®j‚ÇÇ, _‚ü© k f g, colimit_mul_mk_eq _ ‚ü®j‚ÇÅ, _‚ü© ‚ü®j‚ÇÉ, _‚ü© k f h,
        colimit_add_mk_eq _ ‚ü®k, _‚ü© ‚ü®k, _‚ü© k (ùüô k) (ùüô k)]
      simp only [CategoryTheory.Functor.map_id]
      erw [left_distrib (F.map f x) (F.map g y) (F.map h z)]
      rfl
    right_distrib := fun x y z => by
      refine Quot.induction_on‚ÇÉ x y z ?_; clear x y z; intro x y z
      obtain ‚ü®j‚ÇÅ, x‚ü© := x; obtain ‚ü®j‚ÇÇ, y‚ü© := y; obtain ‚ü®j‚ÇÉ, z‚ü© := z
      let k := IsFiltered.max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ
      let f := IsFiltered.firstToMax‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ
      let g := IsFiltered.secondToMax‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ
      let h := IsFiltered.thirdToMax‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ
      erw [colimit_add_mk_eq _ ‚ü®j‚ÇÅ, _‚ü© ‚ü®j‚ÇÇ, _‚ü© k f g, colimit_mul_mk_eq _ ‚ü®k, _‚ü© ‚ü®j‚ÇÉ, _‚ü© k (ùüô k) h,
        colimit_mul_mk_eq _ ‚ü®j‚ÇÅ, _‚ü© ‚ü®j‚ÇÉ, _‚ü© k f h, colimit_mul_mk_eq _ ‚ü®j‚ÇÇ, _‚ü© ‚ü®j‚ÇÉ, _‚ü© k g h,
        colimit_add_mk_eq _ ‚ü®k, _‚ü© ‚ü®k, _‚ü© k (ùüô k) (ùüô k)]
      simp only [CategoryTheory.Functor.map_id]
      erw [right_distrib (F.map f x) (F.map g y) (F.map h z)]
      rfl }

/-- The bundled semiring giving the filtered colimit of a diagram. -/
def colimit : SemiRingCat.{max v u} :=
  SemiRingCat.of <| R.{v, u} F

/-- The cocone over the proposed colimit semiring. -/
def colimitCocone : Cocone F where
  pt := colimit.{v, u} F
  Œπ :=
    { app := fun j => ofHom
        { ((MonCat.FilteredColimits.colimitCocone
            (F ‚ãô forget‚ÇÇ SemiRingCat.{max v u} MonCat)).Œπ.app j).hom,
            ((AddCommMonCat.FilteredColimits.colimitCocone
              (F ‚ãô forget‚ÇÇ SemiRingCat.{max v u} AddCommMonCat)).Œπ.app j).hom with }
      naturality := fun {_ _} f => hom_ext <|
        RingHom.coe_inj ((Types.TypeMax.colimitCocone (F ‚ãô forget SemiRingCat)).Œπ.naturality f) }

namespace colimitCoconeIsColimit

variable {F} (t : Cocone F)

/-- Auxiliary definition for `colimitCoconeIsColimit`. -/
def descAddMonoidHom : R F ‚Üí+ t.1 :=
  ((AddCommMonCat.FilteredColimits.colimitCoconeIsColimit.{v, u}
    (F ‚ãô forget‚ÇÇ SemiRingCat AddCommMonCat)).desc
      ((forget‚ÇÇ SemiRingCat AddCommMonCat).mapCocone t)).hom

lemma descAddMonoidHom_quotMk {j : J} (x : F.obj j) :
    descAddMonoidHom t (Quot.mk _ ‚ü®j, x‚ü©) = t.Œπ.app j x :=
  congr_fun ((forget _).congr_map
    ((AddCommMonCat.FilteredColimits.colimitCoconeIsColimit.{v, u}
      (F ‚ãô forget‚ÇÇ SemiRingCat AddCommMonCat)).fac
        ((forget‚ÇÇ SemiRingCat AddCommMonCat).mapCocone t) j)) x

/-- Auxiliary definition for `colimitCoconeIsColimit`. -/
def descMonoidHom : R F ‚Üí* t.1 :=
  ((MonCat.FilteredColimits.colimitCoconeIsColimit.{v, u}
    (F ‚ãô forget‚ÇÇ _ _)).desc ((forget‚ÇÇ _ _).mapCocone t)).hom

lemma descMonoidHom_quotMk {j : J} (x : F.obj j) :
    descMonoidHom t (Quot.mk _ ‚ü®j, x‚ü©) = t.Œπ.app j x :=
  congr_fun ((forget _).congr_map
    ((MonCat.FilteredColimits.colimitCoconeIsColimit.{v, u}
      (F ‚ãô forget‚ÇÇ _ _)).fac ((forget‚ÇÇ _ _).mapCocone t) j)) x

lemma descMonoidHom_apply_eq (x : R F) :
    descMonoidHom t x = descAddMonoidHom t x := by
  obtain ‚ü®j, x‚ü© := x
  rw [descMonoidHom_quotMk t x, descAddMonoidHom_quotMk t x]

end colimitCoconeIsColimit

open colimitCoconeIsColimit in
/-- The proposed colimit cocone is a colimit in `SemiRingCat`. -/
def colimitCoconeIsColimit : IsColimit <| colimitCocone.{v, u} F where
  desc t := ofHom
    { descAddMonoidHom t with
      map_one' := (descMonoidHom_apply_eq t 1).symm.trans (by simp)
      map_mul' x y := by
        change descAddMonoidHom t (x * y) =
          descAddMonoidHom t x * descAddMonoidHom t y
        simp [‚Üê descMonoidHom_apply_eq] }
  fac t j := by ext x; exact descAddMonoidHom_quotMk t x
  uniq t m hm := by
    ext ‚ü®j, x‚ü©
    exact (congr_fun ((forget _).congr_map (hm j)) x).trans
      (descAddMonoidHom_quotMk t x).symm

instance forget‚ÇÇMon_preservesFilteredColimits :
    PreservesFilteredColimits (forget‚ÇÇ SemiRingCat MonCat.{u}) where
  preserves_filtered_colimits {J hJ1 _} :=
    letI : Category J := hJ1
    { preservesColimit := fun {F} =>
        preservesColimit_of_preserves_colimit_cocone (colimitCoconeIsColimit.{u, u} F)
          (MonCat.FilteredColimits.colimitCoconeIsColimit (F ‚ãô forget‚ÇÇ SemiRingCat MonCat.{u})) }

instance forget_preservesFilteredColimits : PreservesFilteredColimits (forget SemiRingCat.{u}) :=
  Limits.comp_preservesFilteredColimits (forget‚ÇÇ SemiRingCat MonCat) (forget MonCat.{u})

end

end SemiRingCat.FilteredColimits

namespace CommSemiRingCat.FilteredColimits

section

-- We use parameters here, mainly so we can have the abbreviation `R` below, without
-- passing around `F` all the time.
variable {J : Type v} [SmallCategory J] [IsFiltered J] (F : J ‚•§ CommSemiRingCat.{max v u})

/-- The colimit of `F ‚ãô forget‚ÇÇ CommSemiRingCat SemiRingCat` in the category `SemiRingCat`.
In the following, we will show that this has the structure of a _commutative_ semiring.
-/
abbrev R : SemiRingCat.{max v u} :=
  SemiRingCat.FilteredColimits.colimit (F ‚ãô forget‚ÇÇ CommSemiRingCat SemiRingCat.{max v u})

instance colimitCommSemiring : CommSemiring.{max v u} <| R.{v, u} F :=
  { (R F).semiring,
    CommMonCat.FilteredColimits.colimitCommMonoid
      (F ‚ãô forget‚ÇÇ CommSemiRingCat CommMonCat.{max v u}) with }

/-- The bundled commutative semiring giving the filtered colimit of a diagram. -/
def colimit : CommSemiRingCat.{max v u} :=
  CommSemiRingCat.of <| R.{v, u} F

/-- The cocone over the proposed colimit commutative semiring. -/
def colimitCocone : Cocone F where
  pt := colimit.{v, u} F
  Œπ :=
    { app := fun X ‚Ü¶ ofHom <| ((SemiRingCat.FilteredColimits.colimitCocone
          (F ‚ãô forget‚ÇÇ CommSemiRingCat SemiRingCat.{max v u})).Œπ.app X).hom
      naturality := fun _ _ f ‚Ü¶ hom_ext <|
        RingHom.coe_inj ((Types.TypeMax.colimitCocone
          (F ‚ãô forget CommSemiRingCat)).Œπ.naturality f) }


/-- The proposed colimit cocone is a colimit in `CommSemiRingCat`. -/
def colimitCoconeIsColimit : IsColimit <| colimitCocone.{v, u} F :=
  isColimitOfReflects (forget‚ÇÇ _ SemiRingCat)
    (SemiRingCat.FilteredColimits.colimitCoconeIsColimit
      (F ‚ãô forget‚ÇÇ CommSemiRingCat SemiRingCat))

instance forget‚ÇÇSemiRing_preservesFilteredColimits :
    PreservesFilteredColimits (forget‚ÇÇ CommSemiRingCat SemiRingCat.{u}) where
  preserves_filtered_colimits {J hJ1 _} :=
    letI : Category J := hJ1
    { preservesColimit := fun {F} =>
        preservesColimit_of_preserves_colimit_cocone (colimitCoconeIsColimit.{u, u} F)
          (SemiRingCat.FilteredColimits.colimitCoconeIsColimit
            (F ‚ãô forget‚ÇÇ CommSemiRingCat SemiRingCat.{u})) }

instance forget_preservesFilteredColimits :
    PreservesFilteredColimits (forget CommSemiRingCat.{u}) :=
  Limits.comp_preservesFilteredColimits (forget‚ÇÇ CommSemiRingCat SemiRingCat)
    (forget SemiRingCat.{u})

end

end CommSemiRingCat.FilteredColimits

namespace RingCat.FilteredColimits

section

-- We use parameters here, mainly so we can have the abbreviation `R` below, without
-- passing around `F` all the time.
variable {J : Type v} [SmallCategory J] [IsFiltered J] (F : J ‚•§ RingCat.{max v u})

/-- The colimit of `F ‚ãô forget‚ÇÇ RingCat SemiRingCat` in the category `SemiRingCat`.
In the following, we will show that this has the structure of a ring.
-/
abbrev R : SemiRingCat.{max v u} :=
  SemiRingCat.FilteredColimits.colimit.{v, u} (F ‚ãô forget‚ÇÇ RingCat SemiRingCat.{max v u})

instance colimitRing : Ring.{max v u} <| R.{v, u} F :=
  { (R F).semiring,
    AddCommGrpCat.FilteredColimits.colimitAddCommGroup.{v, u}
      (F ‚ãô forget‚ÇÇ RingCat AddCommGrpCat.{max v u}) with }

/-- The bundled ring giving the filtered colimit of a diagram. -/
def colimit : RingCat.{max v u} :=
  RingCat.of <| R.{v, u} F

/-- The cocone over the proposed colimit ring. -/
def colimitCocone : Cocone F where
  pt := colimit.{v, u} F
  Œπ :=
    { app := fun X ‚Ü¶ ofHom <| ((SemiRingCat.FilteredColimits.colimitCocone
          (F ‚ãô forget‚ÇÇ RingCat SemiRingCat.{max v u})).Œπ.app X).hom
      naturality := fun _ _ f ‚Ü¶ hom_ext <|
        RingHom.coe_inj ((Types.TypeMax.colimitCocone (F ‚ãô forget RingCat)).Œπ.naturality f) }

/-- The proposed colimit cocone is a colimit in `Ring`. -/
def colimitCoconeIsColimit : IsColimit <| colimitCocone.{v, u} F :=
  isColimitOfReflects (forget‚ÇÇ _ _)
    (SemiRingCat.FilteredColimits.colimitCoconeIsColimit
      (F ‚ãô forget‚ÇÇ RingCat SemiRingCat))

instance forget‚ÇÇSemiRing_preservesFilteredColimits :
    PreservesFilteredColimits (forget‚ÇÇ RingCat SemiRingCat.{u}) where
  preserves_filtered_colimits {J hJ1 _} :=
    letI : Category J := hJ1
    { preservesColimit := fun {F} =>
        preservesColimit_of_preserves_colimit_cocone (colimitCoconeIsColimit.{u, u} F)
          (SemiRingCat.FilteredColimits.colimitCoconeIsColimit
            (F ‚ãô forget‚ÇÇ RingCat SemiRingCat.{u})) }

instance forget_preservesFilteredColimits : PreservesFilteredColimits (forget RingCat.{u}) :=
  Limits.comp_preservesFilteredColimits (forget‚ÇÇ RingCat SemiRingCat) (forget SemiRingCat.{u})

end

end RingCat.FilteredColimits

namespace CommRingCat.FilteredColimits

section

-- We use parameters here, mainly so we can have the abbreviation `R` below, without
-- passing around `F` all the time.
variable {J : Type v} [SmallCategory J] [IsFiltered J] (F : J ‚•§ CommRingCat.{max v u})

/-- The colimit of `F ‚ãô forget‚ÇÇ CommRingCat RingCat` in the category `RingCat`.
In the following, we will show that this has the structure of a _commutative_ ring.
-/
abbrev R : RingCat.{max v u} :=
  RingCat.FilteredColimits.colimit.{v, u} (F ‚ãô forget‚ÇÇ CommRingCat RingCat.{max v u})

instance colimitCommRing : CommRing.{max v u} <| R.{v, u} F :=
  { (R.{v, u} F).ring,
    CommSemiRingCat.FilteredColimits.colimitCommSemiring
      (F ‚ãô forget‚ÇÇ CommRingCat CommSemiRingCat.{max v u}) with }

/-- The bundled commutative ring giving the filtered colimit of a diagram. -/
def colimit : CommRingCat.{max v u} :=
  CommRingCat.of <| R.{v, u} F

/-- The cocone over the proposed colimit commutative ring. -/
def colimitCocone : Cocone F where
  pt := colimit.{v, u} F
  Œπ :=
    { app := fun X ‚Ü¶ ofHom <| ((RingCat.FilteredColimits.colimitCocone
          (F ‚ãô forget‚ÇÇ CommRingCat RingCat.{max v u})).Œπ.app X).hom
      naturality := fun _ _ f ‚Ü¶ hom_ext <|
        RingHom.coe_inj ((Types.TypeMax.colimitCocone (F ‚ãô forget CommRingCat)).Œπ.naturality f) }

/-- The proposed colimit cocone is a colimit in `CommRingCat`. -/
def colimitCoconeIsColimit : IsColimit <| colimitCocone.{v, u} F :=
  isColimitOfReflects (forget‚ÇÇ _ _)
    (RingCat.FilteredColimits.colimitCoconeIsColimit
      (F ‚ãô forget‚ÇÇ CommRingCat RingCat))

instance forget‚ÇÇRing_preservesFilteredColimits :
    PreservesFilteredColimits (forget‚ÇÇ CommRingCat RingCat.{u}) where
  preserves_filtered_colimits {J hJ1 _} :=
    letI : Category J := hJ1
    { preservesColimit := fun {F} =>
        preservesColimit_of_preserves_colimit_cocone (colimitCoconeIsColimit.{u, u} F)
          (RingCat.FilteredColimits.colimitCoconeIsColimit (F ‚ãô forget‚ÇÇ CommRingCat RingCat.{u})) }

instance forget_preservesFilteredColimits : PreservesFilteredColimits (forget CommRingCat.{u}) :=
  Limits.comp_preservesFilteredColimits (forget‚ÇÇ CommRingCat RingCat) (forget RingCat.{u})

omit [IsFiltered J] in
protected lemma nontrivial {F : J ‚•§ CommRingCat.{v}} [IsFilteredOrEmpty J]
    [‚àÄ i, Nontrivial (F.obj i)] {c : Cocone F} (hc : IsColimit c) : Nontrivial c.pt := by
  classical
  cases isEmpty_or_nonempty J
  ¬∑ exact ((isColimitEquivIsInitialOfIsEmpty _ _ hc).to (.of (ULift ‚Ñ§))).hom.domain_nontrivial
  have i := ‚ÄπNonempty J‚Ä∫.some
  refine ‚ü®c.Œπ.app i 0, c.Œπ.app i 1, fun h ‚Ü¶ ?_‚ü©
  have : IsFiltered J := ‚ü®‚ü©
  obtain ‚ü®k, f, e‚ü© :=
    (Types.FilteredColimit.isColimit_eq_iff' (isColimitOfPreserves (forget _) hc) _ _).mp h
  exact zero_ne_one (((F.map f).hom.map_zero.symm.trans e).trans (F.map f).hom.map_one)

omit [IsFiltered J] in
instance {F : J ‚•§ CommRingCat.{v}} [IsFilteredOrEmpty J]
    [HasColimit F] [‚àÄ i, Nontrivial (F.obj i)] : Nontrivial ‚Üë(Limits.colimit F) :=
  FilteredColimits.nontrivial (getColimitCocone F).2

end

end CommRingCat.FilteredColimits
