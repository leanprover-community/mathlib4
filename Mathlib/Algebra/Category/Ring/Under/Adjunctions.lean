/-
Copyright (c) 2025 Ruiqi Chen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ruiqi Chen
-/
import Mathlib.Algebra.Category.Ring.Under.Basic
import Mathlib.Algebra.MvPolynomial.Basic
import Mathlib.RingTheory.TensorProduct.MvPolynomial
import Mathlib.Algebra.MvPolynomial.CommRing
import Mathlib.Algebra.Category.Ring.Adjunctions

/-!
# Adjunctions related to `Under (R : CommRingCat)`

In this file we provide functors `forgetRelative R S : Under S â¥¤ Under R` for `S` an `R`-algebra,
`freeAbsolute R : Type u â¥¤ Under R` the free functor given by polynomials over `R` and
`forget : Under R â¥¤ Type u` the forgetful functor. And prove some basic facts including adjunctions
`tensorProd R S âŠ£ forgetRelative R S` and `freeAbsolute R âŠ£ forget`.
-/

noncomputable section

open TensorProduct CategoryTheory

universe u
variable {R S : CommRingCat.{u}}
variable [Algebra R S]

namespace CommRingCat

variable (R S) in
/-- The forgetful base change functor. -/
@[simps! map_right]
def forgetRelative : Under S â¥¤ Under R := Under.map <| ofHom Algebra.algebraMap

/-- The adjunction between `tensorProd R S` and `forgetRelative R S`. -/
@[simps! unit_app counit_app]
def adjTensorForget : tensorProd R S âŠ£ forgetRelative R S :=
  (Under.mapPushoutAdj (ofHom <| algebraMap R S)).ofNatIsoLeft ((R.tensorProdIsoPushout S).symm)

variable (R) in
/-- The free functor given by polynomials. -/
@[simps! map_right]
def freeAbsolute : Type u â¥¤ Under R where
  obj Ïƒ := R.mkUnder <| MvPolynomial Ïƒ R
  map f := (MvPolynomial.rename f).toUnder
  map_id Ïƒ := congr_arg (fun x => x.toUnder) <| MvPolynomial.rename_id (Ïƒ := Ïƒ) (R := R)
  map_comp f g := congr_arg (fun x => x.toUnder) (MvPolynomial.rename_comp_rename (R := R) f g).symm

@[simp]
lemma freeAbsolute_obj (Ïƒ : Type u) : algebra ((freeAbsolute R).obj Ïƒ) = MvPolynomial.algebra :=
  mkUnder_eq <| MvPolynomial Ïƒ R

@[simp]
lemma freeAbsolute_map {Ïƒ Ï„ : Type u} (f : Ïƒ âŸ¶ Ï„) :
    toAlgHom ((freeAbsolute R).map f) =
    (cast <| congr_argâ‚‚
    (fun instA instB => @AlgHom R (MvPolynomial Ïƒ R) (MvPolynomial Ï„ R) _ _ _ instA instB)
    (mkUnder_eq (MvPolynomial Ïƒ R)).symm
    (mkUnder_eq (MvPolynomial Ï„ R)).symm) (MvPolynomial.rename f)
  := AlgHom.toUnder_eq (MvPolynomial.rename f)

/-- The forgetful functor `Under R â¥¤ CommRingCat â¥¤ Type`. -/
def forget : Under R â¥¤ Type u := Under.forget R â‹™ HasForget.forget

lemma tensorProd_freeAbsolute_tauto : freeAbsolute R â‹™ R.tensorProd S = {
    obj Ïƒ := S.mkUnder <| S âŠ—[R] (MvPolynomial Ïƒ R)
    map f := (Algebra.TensorProduct.map (AlgHom.id S S) (MvPolynomial.rename f)).toUnder
    map_id Ïƒ := by
      simp only
      have : MvPolynomial.rename (ðŸ™ Ïƒ) = AlgHom.id R (MvPolynomial Ïƒ R) :=
        MvPolynomial.rename_id (R := R) (Ïƒ := Ïƒ)
      rw [this, Algebra.TensorProduct.map_id]
      rfl
    map_comp f g := by
      simp only
      have : MvPolynomial.rename (R := R) (f â‰« g) =
        (MvPolynomial.rename g).comp (MvPolynomial.rename f) :=
        (MvPolynomial.rename_comp_rename f g).symm
      rw [this, Algebra.TensorProduct.map_id_comp, AlgHom.toUnder_comp]
    } := by
  apply CategoryTheory.Functor.ext
  Â· intro Ïƒ Ï„ f
    unfold freeAbsolute tensorProd
    dsimp
    rw [AlgHom.toUnder_eq]
    -- find out the path induction
    have (ninstÏƒ : Algebra R (MvPolynomial Ïƒ R)) (ninstÏ„ : Algebra R (MvPolynomial Ï„ R))
        (eqÏƒ : ninstÏƒ = MvPolynomial.algebra) (eqÏ„ : ninstÏ„ = MvPolynomial.algebra) :
        (@Algebra.TensorProduct.map _ _ _ _ _ _ _ _ _ _ _ _ _ _
        (ninstÏƒ) _ _ _ _ _ (ninstÏ„) (AlgHom.id S S)
        ((cast <| congr_argâ‚‚ (fun instÏƒ instÏ„ => @AlgHom R (MvPolynomial Ïƒ R)
            (MvPolynomial Ï„ R) _ _ _ instÏƒ instÏ„)
        eqÏƒ.symm eqÏ„.symm) (MvPolynomial.rename f))).toUnder =
        eqToHom (congr_arg
          (fun inst => S.mkUnder <| @TensorProduct R _ S (MvPolynomial Ïƒ R) _ _ _
          (@Algebra.toModule _ _ _ _ inst)) <| eqÏƒ) â‰«
        (@Algebra.TensorProduct.map _ _ _ _ _ _ _ _ _ _ _ _ _ _ MvPolynomial.algebra _ _ _ _ _
          MvPolynomial.algebra (AlgHom.id S S) (MvPolynomial.rename f)).toUnder â‰«
        eqToHom (congr_arg
          (fun inst => S.mkUnder <| @TensorProduct R _ S (MvPolynomial Ï„ R) _ _ _
          (@Algebra.toModule _ _ _ _ inst)) <| eqÏ„).symm := by
      subst eqÏƒ eqÏ„
      rfl
    exact this (algebra (R.mkUnder (MvPolynomial Ïƒ R))) (algebra (R.mkUnder (MvPolynomial Ï„ R)))
      (mkUnder_eq (MvPolynomial Ïƒ R)) (mkUnder_eq (MvPolynomial Ï„ R))

/-- We obtain `freeAbsolute S` by base changing `freeAbsolute R` using `âŠ—[R] S`. -/
def tensorProd_freeAbsolute : freeAbsolute R â‹™ R.tensorProd S â‰… freeAbsolute S := by
  -- To get rid of algebra_eq
  rw [tensorProd_freeAbsolute_tauto]
  exact (NatIso.ofComponents
    (fun Ïƒ => (MvPolynomial.algebraTensorAlgEquiv (Ïƒ := Ïƒ) R S).symm.toUnder)
    (fun {Ïƒ Ï„} f => by
      show (MvPolynomial.rename f).toUnder â‰«
          (MvPolynomial.algebraTensorAlgEquiv R S).symm.toAlgHom.toUnder
          = (MvPolynomial.algebraTensorAlgEquiv R S).symm.toAlgHom.toUnder â‰«
          (Algebra.TensorProduct.map (AlgHom.id S S) (MvPolynomial.rename f)).toUnder
      suffices (MvPolynomial.algebraTensorAlgEquiv R S).symm.toAlgHom.comp (MvPolynomial.rename f)
          = (Algebra.TensorProduct.map (AlgHom.id S S) (MvPolynomial.rename f)).comp
            (MvPolynomial.algebraTensorAlgEquiv R S).symm.toAlgHom from
        congr_arg (fun f => f.toUnder) this
      suffices (e : Ïƒ) â†’
          (MvPolynomial.algebraTensorAlgEquiv R S).symm.toAlgHom
          ((MvPolynomial.rename f) (MvPolynomial.X e))
          = (Algebra.TensorProduct.map (AlgHom.id S S) (MvPolynomial.rename f))
          ((MvPolynomial.algebraTensorAlgEquiv R S).symm.toAlgHom (MvPolynomial.X e)) from by
        exact MvPolynomial.algHom_ext this
      unfold MvPolynomial.algebraTensorAlgEquiv
      simp only [AlgEquiv.toAlgHom_eq_coe, MvPolynomial.rename_X, AlgHom.coe_coe,
        AlgEquiv.ofAlgHom_symm_apply, MvPolynomial.aeval_X, Algebra.TensorProduct.map_tmul,
        AlgHom.coe_id, id_eq, implies_true]
    )).symm

/-- A commutative ring is an algebra over `â„¤` which is commutative. -/
def Under_â„¤ : Under (of (ULift.{u, 0} â„¤)) â‰Œ CommRingCat.{u} :=
  Under.equivalenceOfIsInitial isInitial

/-- The defined `freeAbsolute â„¤` is isomorphic to `free` -/
def freeAbsolute_â„¤_tauto : free â‹™ Under_â„¤.inverse â‰… freeAbsolute (of (ULift.{u, 0} â„¤)) where
  hom := {
    app Ïƒ := Under.homMk
      (CommRingCat.ofHom <| MvPolynomial.map <| Int.castRingHom (ULift.{u, 0} â„¤))
      (Limits.IsInitial.hom_ext isInitial _ _)
    naturality {Ïƒ Ï„} f := by
      apply Under.UnderMorphism.ext
      exact hom_ext <| MvPolynomial.map_comp_rename (Int.castRingHom (ULift.{u, 0} â„¤)) f
  }
  inv := {
    app Ïƒ := Under.homMk (CommRingCat.ofHom <| MvPolynomial.map RingHom.smulOneHom)
      (Limits.IsInitial.hom_ext isInitial _ _)
    naturality {Ïƒ Ï„} f := by
      apply Under.UnderMorphism.ext
      exact hom_ext <| MvPolynomial.map_comp_rename RingHom.smulOneHom f
  }
  hom_inv_id := by
    ext Ïƒ (x : MvPolynomial Ïƒ â„¤)
    show (MvPolynomial.map RingHom.smulOneHom)
      ((MvPolynomial.map (Int.castRingHom (ULift.{u, 0} â„¤))) x) = x
    rw [MvPolynomial.map_map _ (RingHom.smulOneHom (R := (ULift.{u, 0} â„¤)))]
    have : RingHom.smulOneHom.comp (Int.castRingHom (ULift.{u, 0} â„¤)) = RingHom.id â„¤ := by aesop_cat
    rw [this]
    exact MvPolynomial.map_id x
  inv_hom_id := by
    ext Ïƒ (x : MvPolynomial Ïƒ (ULift.{u, 0} â„¤))
    show (MvPolynomial.map (Int.castRingHom (ULift.{u, 0} â„¤)))
      ((MvPolynomial.map RingHom.smulOneHom) x) = x
    rw [MvPolynomial.map_map]
    have : (Int.castRingHom (ULift.{u, 0} â„¤)).comp (RingHom.smulOneHom (R := ULift.{u, 0} â„¤))
        = RingHom.id (ULift.{u, 0} â„¤) := by aesop_cat
    rw [this]
    exact MvPolynomial.map_id x

instance (R : CommRingCat.{u}) : Algebra (of (ULift.{u, 0} â„¤)) R
  := RingHom.toAlgebra RingHom.smulOneHom

/-- The free forgetful adjunction of `Under R`. -/
def adjFreeForget : freeAbsolute R âŠ£ forget :=
  (adj.comp (Under_â„¤.symm.toAdjunction.comp adjTensorForget)).ofNatIsoLeft
  (isoWhiskerRight freeAbsolute_â„¤_tauto ((of (ULift.{u, 0} â„¤)).tensorProd R)
      â‰ªâ‰« tensorProd_freeAbsolute)

end CommRingCat
