/-
Copyright (c) 2025 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.Algebra.Category.ModuleCat.Limits
import Mathlib.Algebra.Small.Module

/-!
# A categorical description of the center of a ring

In this file we prove that the center of a ring `R` is isomorphic to `End (ùü≠ R-Mod)` the
endomorphism ring of the identity functor on the category of `R`-modules. Consequently, the ring
structure of a commutative ring is completely determined by its module category.

## Main results

- `Subring.centerEquivEndIdFunctor`: the center of a ring `R` is isomorphic to `End (ùü≠ R-Mod)`.
- `RingEquiv.ofModuleCatEquiv`: if two commutative rings have equivalent module categories, they are
  isomorphic as rings.

-/

universe v v'

variable (R : Type*) [Ring R]

open CategoryTheory

/--
For any ring `R`, the center of `R` is isomorphic to `End (ùü≠ (ModuleCat R))`, the endomorphism ring
of the identity functor on the category of `R`-modules.
-/
@[simps]
noncomputable def Subring.centerEquivEndIdFunctor [Small.{v} R] :
    center R ‚âÉ+* End (ùü≠ (ModuleCat.{v} R)) where
  toFun x :=
  { app M := ModuleCat.ofHom
      { toFun := (x.1 ‚Ä¢ ¬∑)
        map_add' := by aesop
        map_smul' r := by simp [‚Üê mul_smul, mem_center_iff.1 x.2 r] } }
  invFun f := centerMulOppositeEquiv <| centerCongr
    ((ModuleCat.of R (Shrink.{v} R)).endRingEquiv.trans
      ((Module.moduleEndSelf R).trans (linearEquivShrink R R).conjRingEquiv).symm)
    ‚ü®f.app _, mem_center_iff.mpr fun g ‚Ü¶ (f.naturality _).symm‚ü©
  left_inv r := Subtype.ext <| show (linearEquivShrink ..).symm (r.1 ‚Ä¢ _) = _ by
    rw [map_smul, LinearEquiv.coe_toLinearMap, LinearEquiv.symm_apply_apply, smul_eq_mul, mul_one]
  right_inv f := by
    apply NatTrans.ext
    ext M (m : M)
    simpa [linearEquivShrink, Equiv.linearEquiv] using
      congr($(f.naturality (X := .of R <| Shrink.{v} R) (Y := .of R M) <|
        ModuleCat.ofHom <| LinearMap.toSpanSingleton R M m ‚àò‚Çó (linearEquivShrink R R).symm).hom
      (equivShrink R 1)).symm
  map_mul' x y := by
    apply NatTrans.ext
    ext M (m : M)
    exact mul_smul x.1 y.1 m
  map_add' x y := by
    apply NatTrans.ext
    ext M (m : M)
    exact add_smul x.1 y.1 m

/--
For any two commutative rings `R` and `S`, if the categories of `R`-modules and `S`-modules are
equivalent, then `R` and `S` are isomorphic as rings.
-/
noncomputable def RingEquiv.ofModuleCatEquiv {R S : Type*} [CommRing R] [CommRing S]
    [Small.{v} R] [Small.{v'} S]
    (e : ModuleCat.{v} R ‚âå ModuleCat.{v'} S) : R ‚âÉ+* S :=
  letI : e.functor.Additive := Functor.additive_of_preserves_binary_products e.functor
  let i‚ÇÅ : R ‚âÉ+* (‚ä§ : Subring R) := Subring.topEquiv.symm
  let i‚ÇÇ : (‚ä§ : Subring R) ‚âÉ+* Subring.center R := Subring.center_eq_top R ‚ñ∏ .refl _
  let i‚ÇÉ : End (ùü≠ (ModuleCat.{v} R)) ‚âÉ+* End (ùü≠ (ModuleCat.{v'} S)) :=
    Equivalence.endRingEquiv (e := e) (e' := e) (by rfl)
  let i‚ÇÑ : Subring.center S ‚âÉ+* (‚ä§ : Subring S) := Subring.center_eq_top S ‚ñ∏ .refl _
  let i‚ÇÖ : (‚ä§ : Subring S) ‚âÉ+* S := Subring.topEquiv
  i‚ÇÅ.trans <| i‚ÇÇ.trans <| (Subring.centerEquivEndIdFunctor R).trans <|
    i‚ÇÉ.trans <| (Subring.centerEquivEndIdFunctor S).symm.trans <| i‚ÇÑ.trans i‚ÇÖ
