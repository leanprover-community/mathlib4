/-
Copyright (c) 2025 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import Mathlib.CategoryTheory.Endomorphism
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.Algebra.Category.ModuleCat.ChangeOfRings
import Mathlib.Algebra.Ring.Subring.Basic
import Mathlib.RingTheory.Ideal.Span

/-!
# A categorical description of the center of a ring

In this file we prove that the center of a ring `R` is isomorphic to `End (ğŸ­ R-Mod)` the
endomorphism ring of the identity functor on the category of `R`-modules. Consequently, the ring
structure of a commutative ring is complete determined by its module category.

## Main results

- `Subring.centerEquivEndIdFunctor`: the center of a ring `R` is isomorphic to `End (ğŸ­ R-Mod)`.
- `RingEquiv.ofModuleCatEquiv`: if two commutative rings have equivalent module categories, they are
  isomorphic as rings.

-/

universe u u'

variable (R : Type u) [Ring R]

open CategoryTheory

/--
For any ring `R`, the center of `R` is isomorphic to `End (ğŸ­ (ModuleCat R))`, the endomorphism ring
of the identity functor on the category of `R`-modules.
-/
@[simps]
def Subring.centerEquivEndIdFunctor : Subring.center R â‰ƒ+* End (ğŸ­ (ModuleCat.{u} R)) where
  toFun x :=
    { app M := ModuleCat.ofHom
        { toFun := (x.1 â€¢ Â·)
          map_add' := by aesop
          map_smul' r m := by simp [â† mul_smul, Subring.mem_center_iff.1 x.2 r] } }
  map_add' x y := by
    apply NatTrans.ext
    ext M (m : M)
    exact add_smul x.1 y.1 m
  map_mul' x y := by
    apply NatTrans.ext
    ext M (m : M)
    exact mul_smul x.1 y.1 m
  invFun f := âŸ¨f.app (.of R R) |>.hom (1 : R), by
    rw [Subring.mem_center_iff]
    intro r
    have := congr($(f.naturality (X := .of R R) (Y := .of R R) (ModuleCat.ofHom
      { toFun x := x * r
        map_add' := by simp [add_mul]
        map_smul' := by simp [mul_assoc] })).hom (1 : R))
    simp only [Functor.id_obj, Functor.id_map, ModuleCat.hom_comp, LinearMap.coe_comp,
      LinearMap.coe_mk, AddHom.coe_mk, Function.comp_apply, one_mul] at this
    erw [â† this]
    have := (f.app (ModuleCat.of R R)).hom.map_smul r (1 : R)
    simp only [Functor.id_obj, smul_eq_mul, mul_one] at this
    rw [this]
    rflâŸ©
  left_inv x := by simp
  right_inv f := by
    apply NatTrans.ext
    ext M (m : M)
    simp only [Functor.id_obj, LinearMap.coe_mk, AddHom.coe_mk]
    have := congr($(f.naturality (X := .of R R) (Y := .of R M)
      (ModuleCat.ofHom
        { toFun x := x â€¢ m
          map_add' := by simp [add_smul]
          map_smul' x y := by simp [mul_smul] })).hom (1 : R))
    simp only [ModuleCat.of_coe, Functor.id_obj, Functor.id_map, ModuleCat.hom_comp,
      LinearMap.coe_comp, LinearMap.coe_mk, AddHom.coe_mk, Function.comp_apply,
      one_smul] at this
    exact this.symm

/--
For any two commutative rings `R` and `S`, if the categories of `R`-modules and `S`-modules are
equivalent, then `R` and `S` are isomorphic as rings.
-/
def RingEquiv.ofModuleCatEquiv {R : Type u} {S : Type u'} [CommRing R] [CommRing S]
    (e : ModuleCat.{u} R â‰Œ ModuleCat.{u'} S) : R â‰ƒ+* S :=
  letI : e.functor.Additive := Functor.additive_of_preserves_binary_products e.functor
  let iâ‚ : R â‰ƒ+* (âŠ¤ : Subring R) := Subring.topEquiv.symm
  let iâ‚‚ : (âŠ¤ : Subring R) â‰ƒ+* Subring.center R := Subring.center_eq_top R â–¸ .refl _
  let iâ‚„ : Subring.center S â‰ƒ+* (âŠ¤ : Subring S) := Subring.center_eq_top S â–¸ .refl _
  let iâ‚… : (âŠ¤ : Subring S) â‰ƒ+* S := Subring.topEquiv
  let i : End (ğŸ­ (ModuleCat.{u} R)) â‰ƒ+* End (ğŸ­ (ModuleCat.{u'} S)) :=
  { toFun f := .of
      { app N :=
          e.counitInv.app N â‰« e.functor.map (f.app (e.inverse.obj N)) â‰«
          e.counit.app N
        naturality M N g := by
          -- Is `aesop_cat` expected to solve this?
          have eq :=
            e.counitInv.app M â‰«=
            congr(e.functor.map $(f.naturality (e.inverse.map g))) =â‰«
            e.counit.app N
          simpa using eq}
    invFun f := .of
      { app N := e.unit.app N â‰« e.inverse.map (f.app (e.functor.obj N)) â‰« e.unitInv.app N
        naturality M N g := by
          -- Is `aesop_cat` expected to solve this?
          have eq :=
            e.unit.app M â‰«=
            congr(e.inverse.map $(f.naturality (e.functor.map g))) =â‰«
            e.unitInv.app N
          simp only [Functor.id_obj, Functor.comp_obj, Functor.id_map, Functor.map_comp,
            Equivalence.inv_fun_map, Category.assoc, Iso.hom_inv_id_app_assoc,
            Iso.hom_inv_id_app, Category.comp_id] at eq
          simpa using eq }
    left_inv f := by
      apply NatTrans.ext
      ext N : 1
      have eq := f.naturality (e.unit.app N) =â‰« e.unitInv.app N
      simpa [End.of] using eq
    right_inv f := by
      apply NatTrans.ext
      ext N : 1
      have eq := e.counitInv.app N â‰«= f.naturality (e.counit.app N) |>.symm
      simpa [End.of] using eq
    map_mul' f g := by
      apply NatTrans.ext
      ext N : 1
      simp only [Functor.id_obj, Functor.comp_obj, End.mul_def, NatTrans.comp_app, Functor.map_comp,
        Category.assoc]
      simp [End.of]
    map_add' f g := by
      apply NatTrans.ext
      ext N : 1
      change _â‰« e.functor.map (_ + _) â‰« _  = _ â‰« _ + _ â‰« _
      simp [Functor.map_add] }
  iâ‚.trans <| iâ‚‚.trans <| (Subring.centerEquivEndIdFunctor R).trans <|
    i.trans <| (Subring.centerEquivEndIdFunctor S).symm.trans <| iâ‚„.trans iâ‚…
