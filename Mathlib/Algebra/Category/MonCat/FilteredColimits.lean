/-
Copyright (c) 2021 Justus Springer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Justus Springer
-/
import Mathlib.Algebra.Category.MonCat.Limits
import Mathlib.CategoryTheory.Limits.Preserves.Filtered
import Mathlib.CategoryTheory.ConcreteCategory.Elementwise
import Mathlib.CategoryTheory.Limits.TypesFiltered

#align_import algebra.category.Mon.filtered_colimits from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# The forgetful functor from (commutative) (additive) monoids preserves filtered colimits.

Forgetful functors from algebraic categories usually don't preserve colimits. However, they tend
to preserve _filtered_ colimits.

In this file, we start with a small filtered category `J` and a functor `F : J â¥¤ MonCat`.
We then construct a monoid structure on the colimit of `F â‹™ forget MonCat` (in `Type`), thereby
showing that the forgetful functor `forget MonCat` preserves filtered colimits. Similarly for
`AddMonCat`, `CommMonCat` and `AddCommMonCat`.

-/

set_option linter.uppercaseLean3 false

universe v u

noncomputable section

open scoped Classical

open CategoryTheory

open CategoryTheory.Limits

open CategoryTheory.IsFiltered renaming max â†’ max' -- avoid name collision with `_root_.max`.

namespace MonCat.FilteredColimits

section

-- Porting note: mathlib 3 used `parameters` here, mainly so we can have the abbreviations `M` and
-- `M.mk` below, without passing around `F` all the time.
variable {J : Type v} [SmallCategory J] (F : J â¥¤ MonCatMax.{v, u})

/-- The colimit of `F â‹™ forget MonCat` in the category of types.
In the following, we will construct a monoid structure on `M`.
-/
@[to_additive
      "The colimit of `F â‹™ forget AddMon` in the category of types.
      In the following, we will construct an additive monoid structure on `M`."]
abbrev M :=
  Types.Quot (F â‹™ forget MonCat)
#align Mon.filtered_colimits.M MonCat.FilteredColimits.M
#align AddMon.filtered_colimits.M AddMonCat.FilteredColimits.M

/-- The canonical projection into the colimit, as a quotient type. -/
@[to_additive "The canonical projection into the colimit, as a quotient type."]
noncomputable abbrev M.mk : (Î£ j, F.obj j) â†’ M.{v, u} F :=
  Quot.mk _
#align Mon.filtered_colimits.M.mk MonCat.FilteredColimits.M.mk
#align AddMon.filtered_colimits.M.mk AddMonCat.FilteredColimits.M.mk

@[to_additive]
theorem M.mk_eq (x y : Î£ j, F.obj j)
    (h : âˆƒ (k : J) (f : x.1 âŸ¶ k) (g : y.1 âŸ¶ k), F.map f x.2 = F.map g y.2) :
    M.mk.{v, u} F x = M.mk F y :=
  Quot.EqvGen_sound (Types.FilteredColimit.eqvGen_quot_rel_of_rel (F â‹™ forget MonCat) x y h)
#align Mon.filtered_colimits.M.mk_eq MonCat.FilteredColimits.M.mk_eq
#align AddMon.filtered_colimits.M.mk_eq AddMonCat.FilteredColimits.M.mk_eq

variable [IsFiltered J]

/-- As `J` is nonempty, we can pick an arbitrary object `jâ‚€ : J`. We use this object to define the
"one" in the colimit as the equivalence class of `âŸ¨jâ‚€, 1 : F.obj jâ‚€âŸ©`.
-/
@[to_additive
  "As `J` is nonempty, we can pick an arbitrary object `jâ‚€ : J`. We use this object to
  define the \"zero\" in the colimit as the equivalence class of `âŸ¨jâ‚€, 0 : F.obj jâ‚€âŸ©`."]
noncomputable instance colimitOne :
  One (M.{v, u} F) where one := M.mk F âŸ¨IsFiltered.nonempty.some,1âŸ©
#align Mon.filtered_colimits.colimit_has_one MonCat.FilteredColimits.colimitOne
#align AddMon.filtered_colimits.colimit_has_zero AddMonCat.FilteredColimits.colimitZero

/-- The definition of the "one" in the colimit is independent of the chosen object of `J`.
In particular, this lemma allows us to "unfold" the definition of `colimit_one` at a custom chosen
object `j`.
-/
@[to_additive
      "The definition of the \"zero\" in the colimit is independent of the chosen object
      of `J`. In particular, this lemma allows us to \"unfold\" the definition of `colimit_zero` at
      a custom chosen object `j`."]
theorem colimit_one_eq (j : J) : (1 : M.{v, u} F) = M.mk F âŸ¨j, 1âŸ© := by
  apply M.mk_eq
  refine âŸ¨max' _ j, IsFiltered.leftToMax _ j, IsFiltered.rightToMax _ j, ?_âŸ©
  simp
#align Mon.filtered_colimits.colimit_one_eq MonCat.FilteredColimits.colimit_one_eq
#align AddMon.filtered_colimits.colimit_zero_eq AddMonCat.FilteredColimits.colimit_zero_eq

/-- The "unlifted" version of multiplication in the colimit. To multiply two dependent pairs
`âŸ¨jâ‚, xâŸ©` and `âŸ¨jâ‚‚, yâŸ©`, we pass to a common successor of `jâ‚` and `jâ‚‚` (given by `IsFiltered.max`)
and multiply them there.
-/
@[to_additive
      "The \"unlifted\" version of addition in the colimit. To add two dependent pairs
      `âŸ¨jâ‚, xâŸ©` and `âŸ¨jâ‚‚, yâŸ©`, we pass to a common successor of `jâ‚` and `jâ‚‚`
      (given by `IsFiltered.max`) and add them there."]
noncomputable def colimitMulAux (x y : Î£ j, F.obj j) : M.{v, u} F :=
  M.mk F âŸ¨IsFiltered.max x.fst y.fst, F.map (IsFiltered.leftToMax x.1 y.1) x.2 *
    F.map (IsFiltered.rightToMax x.1 y.1) y.2âŸ©
#align Mon.filtered_colimits.colimit_mul_aux MonCat.FilteredColimits.colimitMulAux
#align AddMon.filtered_colimits.colimit_add_aux AddMonCat.FilteredColimits.colimitAddAux

/-- Multiplication in the colimit is well-defined in the left argument. -/
@[to_additive "Addition in the colimit is well-defined in the left argument."]
theorem colimitMulAux_eq_of_rel_left {x x' y : Î£ j, F.obj j}
    (hxx' : Types.FilteredColimit.Rel (F â‹™ forget MonCat) x x') :
    colimitMulAux.{v, u} F x y = colimitMulAux.{v, u} F x' y := by
  cases' x with jâ‚ x; cases' y with jâ‚‚ y; cases' x' with jâ‚ƒ x'
  obtain âŸ¨l, f, g, hfgâŸ© := hxx'
  simp? at hfg says simp only [Functor.comp_obj, Functor.comp_map, forget_map] at hfg
  obtain âŸ¨s, Î±, Î², Î³, hâ‚, hâ‚‚, hâ‚ƒâŸ© :=
    IsFiltered.tulip (IsFiltered.leftToMax jâ‚ jâ‚‚) (IsFiltered.rightToMax jâ‚ jâ‚‚)
      (IsFiltered.rightToMax jâ‚ƒ jâ‚‚) (IsFiltered.leftToMax jâ‚ƒ jâ‚‚) f g
  apply M.mk_eq
  use s, Î±, Î³
  dsimp
  simp_rw [MonoidHom.map_mul]
  -- Porting note: Lean cannot seem to use lemmas from concrete categories directly
  change (F.map _ â‰« F.map _) _ * (F.map _ â‰« F.map _) _ =
    (F.map _ â‰« F.map _) _ * (F.map _ â‰« F.map _) _
  simp_rw [â† F.map_comp, hâ‚, hâ‚‚, hâ‚ƒ, F.map_comp]
  congr 1
  change F.map _ (F.map _ _) = F.map _ (F.map _ _)
  rw [hfg]
#align Mon.filtered_colimits.colimit_mul_aux_eq_of_rel_left MonCat.FilteredColimits.colimitMulAux_eq_of_rel_left
#align AddMon.filtered_colimits.colimit_add_aux_eq_of_rel_left AddMonCat.FilteredColimits.colimitAddAux_eq_of_rel_left

/-- Multiplication in the colimit is well-defined in the right argument. -/
@[to_additive "Addition in the colimit is well-defined in the right argument."]
theorem colimitMulAux_eq_of_rel_right {x y y' : Î£ j, F.obj j}
    (hyy' : Types.FilteredColimit.Rel (F â‹™ forget MonCat) y y') :
    colimitMulAux.{v, u} F x y = colimitMulAux.{v, u} F x y' := by
  cases' y with jâ‚ y; cases' x with jâ‚‚ x; cases' y' with jâ‚ƒ y'
  obtain âŸ¨l, f, g, hfgâŸ© := hyy'
  simp only [Functor.comp_obj, Functor.comp_map, forget_map] at hfg
  obtain âŸ¨s, Î±, Î², Î³, hâ‚, hâ‚‚, hâ‚ƒâŸ© :=
    IsFiltered.tulip (IsFiltered.rightToMax jâ‚‚ jâ‚) (IsFiltered.leftToMax jâ‚‚ jâ‚)
      (IsFiltered.leftToMax jâ‚‚ jâ‚ƒ) (IsFiltered.rightToMax jâ‚‚ jâ‚ƒ) f g
  apply M.mk_eq
  use s, Î±, Î³
  dsimp
  simp_rw [MonoidHom.map_mul]
  -- Porting note: Lean cannot seem to use lemmas from concrete categories directly
  change (F.map _ â‰« F.map _) _ * (F.map _ â‰« F.map _) _ =
    (F.map _ â‰« F.map _) _ * (F.map _ â‰« F.map _) _
  simp_rw [â† F.map_comp, hâ‚, hâ‚‚, hâ‚ƒ, F.map_comp]
  congr 1
  change F.map _ (F.map _ _) = F.map _ (F.map _ _)
  rw [hfg]
#align Mon.filtered_colimits.colimit_mul_aux_eq_of_rel_right MonCat.FilteredColimits.colimitMulAux_eq_of_rel_right
#align AddMon.filtered_colimits.colimit_add_aux_eq_of_rel_right AddMonCat.FilteredColimits.colimitAddAux_eq_of_rel_right

/-- Multiplication in the colimit. See also `colimitMulAux`. -/
@[to_additive "Addition in the colimit. See also `colimitAddAux`."]
noncomputable instance colimitMul : Mul (M.{v, u} F) :=
{ mul := fun x y => by
    refine Quot.liftâ‚‚ (colimitMulAux F) ?_ ?_ x y
    Â· intro x y y' h
      apply colimitMulAux_eq_of_rel_right
      apply Types.FilteredColimit.rel_of_quot_rel
      exact h
    Â· intro x x' y h
      apply colimitMulAux_eq_of_rel_left
      apply Types.FilteredColimit.rel_of_quot_rel
      exact h }
#align Mon.filtered_colimits.colimit_has_mul MonCat.FilteredColimits.colimitMul
#align AddMon.filtered_colimits.colimit_has_add AddMonCat.FilteredColimits.colimitAdd

/-- Multiplication in the colimit is independent of the chosen "maximum" in the filtered category.
In particular, this lemma allows us to "unfold" the definition of the multiplication of `x` and `y`,
using a custom object `k` and morphisms `f : x.1 âŸ¶ k` and `g : y.1 âŸ¶ k`.
-/
@[to_additive
      "Addition in the colimit is independent of the chosen \"maximum\" in the filtered
      category. In particular, this lemma allows us to \"unfold\" the definition of the addition of
      `x` and `y`, using a custom object `k` and morphisms `f : x.1 âŸ¶ k` and `g : y.1 âŸ¶ k`."]
theorem colimit_mul_mk_eq (x y : Î£ j, F.obj j) (k : J) (f : x.1 âŸ¶ k) (g : y.1 âŸ¶ k) :
    M.mk.{v, u} F x * M.mk F y = M.mk F âŸ¨k, F.map f x.2 * F.map g y.2âŸ© := by
  cases' x with jâ‚ x; cases' y with jâ‚‚ y
  obtain âŸ¨s, Î±, Î², hâ‚, hâ‚‚âŸ© := IsFiltered.bowtie (IsFiltered.leftToMax jâ‚ jâ‚‚) f
    (IsFiltered.rightToMax jâ‚ jâ‚‚) g
  refine M.mk_eq F _ _ ?_
  use s, Î±, Î²
  dsimp
  simp_rw [MonoidHom.map_mul]
  -- Porting note: Lean cannot seem to use lemmas from concrete categories directly
  change (F.map _ â‰« F.map _) _ * (F.map _ â‰« F.map _) _ =
    (F.map _ â‰« F.map _) _ * (F.map _ â‰« F.map _) _
  simp_rw [â† F.map_comp, hâ‚, hâ‚‚]
#align Mon.filtered_colimits.colimit_mul_mk_eq MonCat.FilteredColimits.colimit_mul_mk_eq
#align AddMon.filtered_colimits.colimit_add_mk_eq AddMonCat.FilteredColimits.colimit_add_mk_eq

@[to_additive]
noncomputable instance colimitMulOneClass : MulOneClass (M.{v, u} F) :=
  { colimitOne F,
    colimitMul F with
    one_mul := fun x => by
      refine Quot.inductionOn x ?_
      intro x
      cases' x with j x
      rw [colimit_one_eq F j, colimit_mul_mk_eq F âŸ¨j, 1âŸ© âŸ¨j, xâŸ© j (ðŸ™ j) (ðŸ™ j), MonoidHom.map_one,
        one_mul, F.map_id]
      -- Porting note: `id_apply` does not work here, but the two sides are def-eq
      rfl
    mul_one := fun x => by
      refine Quot.inductionOn x ?_
      intro x
      cases' x with j x
      rw [colimit_one_eq F j, colimit_mul_mk_eq F âŸ¨j, xâŸ© âŸ¨j, 1âŸ© j (ðŸ™ j) (ðŸ™ j), MonoidHom.map_one,
        mul_one, F.map_id]
      -- Porting note: `id_apply` does not work here, but the two sides are def-eq
      rfl }

@[to_additive]
noncomputable instance colimitMonoid : Monoid (M.{v, u} F) :=
  { colimitMulOneClass F with
    mul_assoc := fun x y z => by
      refine Quot.induction_onâ‚ƒ x y z ?_
      clear x y z
      intro x y z
      cases' x with jâ‚ x
      cases' y with jâ‚‚ y
      cases' z with jâ‚ƒ z
      change M.mk F _ * M.mk F _ * M.mk F _ = M.mk F _ * M.mk F _
      dsimp
      rw [colimit_mul_mk_eq F âŸ¨jâ‚, xâŸ© âŸ¨jâ‚‚, yâŸ© (IsFiltered.max jâ‚ (IsFiltered.max jâ‚‚ jâ‚ƒ))
          (IsFiltered.leftToMax jâ‚ (IsFiltered.max jâ‚‚ jâ‚ƒ))
          (IsFiltered.leftToMax jâ‚‚ jâ‚ƒ â‰« IsFiltered.rightToMax _ _),
        colimit_mul_mk_eq F âŸ¨(IsFiltered.max jâ‚ (IsFiltered.max jâ‚‚ jâ‚ƒ)), _âŸ© âŸ¨jâ‚ƒ, zâŸ©
          (IsFiltered.max jâ‚ (IsFiltered.max jâ‚‚ jâ‚ƒ)) (ðŸ™ _)
          (IsFiltered.rightToMax jâ‚‚ jâ‚ƒ â‰« IsFiltered.rightToMax _ _),
        colimit_mul_mk_eq.{v, u} F âŸ¨jâ‚, xâŸ© âŸ¨IsFiltered.max jâ‚‚ jâ‚ƒ, _âŸ© _
          (IsFiltered.leftToMax _ _) (IsFiltered.rightToMax _ _)]
      congr 2
      dsimp only
      rw [F.map_id, show âˆ€ x, (ðŸ™ (F.obj (IsFiltered.max jâ‚ (IsFiltered.max jâ‚‚ jâ‚ƒ)))) x = x
        from fun _ => rfl, mul_assoc, MonoidHom.map_mul, F.map_comp, F.map_comp]
      rfl }
#align Mon.filtered_colimits.colimit_monoid MonCat.FilteredColimits.colimitMonoid
#align AddMon.filtered_colimits.colimit_add_monoid AddMonCat.FilteredColimits.colimitAddMonoid

/-- The bundled monoid giving the filtered colimit of a diagram. -/
@[to_additive
  "The bundled additive monoid giving the filtered colimit of a diagram."]
noncomputable def colimit : MonCat.{max v u} :=
  MonCat.of (M.{v, u} F)
#align Mon.filtered_colimits.colimit MonCat.FilteredColimits.colimit
#align AddMon.filtered_colimits.colimit AddMonCat.FilteredColimits.colimit

/-- The monoid homomorphism from a given monoid in the diagram to the colimit monoid. -/
@[to_additive
      "The additive monoid homomorphism from a given additive monoid in the diagram to the
      colimit additive monoid."]
def coconeMorphism (j : J) : F.obj j âŸ¶ colimit F where
  toFun := (Types.TypeMax.colimitCocone.{v, max v u, v} (F â‹™ forget MonCat)).Î¹.app j
  map_one' := (colimit_one_eq F j).symm
  map_mul' x y := by
    convert (colimit_mul_mk_eq F âŸ¨j, xâŸ© âŸ¨j, yâŸ© j (ðŸ™ j) (ðŸ™ j)).symm
    rw [F.map_id]
    rfl
#align Mon.filtered_colimits.cocone_morphism MonCat.FilteredColimits.coconeMorphism
#align AddMon.filtered_colimits.cocone_morphism AddMonCat.FilteredColimits.coconeMorphism

@[to_additive (attr := simp)]
theorem cocone_naturality {j j' : J} (f : j âŸ¶ j') :
    F.map f â‰« coconeMorphism.{v, u} F j' = coconeMorphism F j :=
  MonoidHom.ext fun x =>
    congr_fun ((Types.TypeMax.colimitCocone (F â‹™ forget MonCat)).Î¹.naturality f) x
#align Mon.filtered_colimits.cocone_naturality MonCat.FilteredColimits.cocone_naturality
#align AddMon.filtered_colimits.cocone_naturality AddMonCat.FilteredColimits.cocone_naturality

/-- The cocone over the proposed colimit monoid. -/
@[to_additive "The cocone over the proposed colimit additive monoid."]
noncomputable def colimitCocone : Cocone F where
  pt := colimit.{v, u} F
  Î¹ := { app := coconeMorphism F }
#align Mon.filtered_colimits.colimit_cocone MonCat.FilteredColimits.colimitCocone
#align AddMon.filtered_colimits.colimit_cocone AddMonCat.FilteredColimits.colimitCocone

/-- Given a cocone `t` of `F`, the induced monoid homomorphism from the colimit to the cocone point.
As a function, this is simply given by the induced map of the corresponding cocone in `Type`.
The only thing left to see is that it is a monoid homomorphism.
-/
@[to_additive
      "Given a cocone `t` of `F`, the induced additive monoid homomorphism from the colimit
      to the cocone point. As a function, this is simply given by the induced map of the
      corresponding cocone in `Type`. The only thing left to see is that it is an additive monoid
      homomorphism."]
def colimitDesc (t : Cocone F) : colimit.{v, u} F âŸ¶ t.pt where
  toFun := (Types.TypeMax.colimitCoconeIsColimit.{v, max v u, v} (F â‹™ forget MonCat)).desc
    ((forget MonCat).mapCocone t)
  map_one' := by
    rw [colimit_one_eq F IsFiltered.nonempty.some]
    exact MonoidHom.map_one _
  map_mul' x y := by
    refine Quot.induction_onâ‚‚ x y ?_
    clear x y
    intro x y
    cases' x with i x
    cases' y with j y
    rw [colimit_mul_mk_eq F âŸ¨i, xâŸ© âŸ¨j, yâŸ© (max' i j) (IsFiltered.leftToMax i j)
      (IsFiltered.rightToMax i j)]
    dsimp [Types.TypeMax.colimitCoconeIsColimit]
    -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
    erw [MonoidHom.map_mul]
    -- Porting note: `rw` can't see through coercion is actually forgetful functor,
    -- so can't rewrite `t.w_apply`
    congr 1 <;>
    exact t.w_apply _ _
#align Mon.filtered_colimits.colimit_desc MonCat.FilteredColimits.colimitDesc
#align AddMon.filtered_colimits.colimit_desc AddMonCat.FilteredColimits.colimitDesc

/-- The proposed colimit cocone is a colimit in `MonCat`. -/
@[to_additive "The proposed colimit cocone is a colimit in `AddMonCat`."]
def colimitCoconeIsColimit : IsColimit (colimitCocone.{v, u} F) where
  desc := colimitDesc.{v, u} F
  fac t j := MonoidHom.ext fun x => congr_fun ((Types.TypeMax.colimitCoconeIsColimit.{v, u}
    (F â‹™ forget MonCat)).fac ((forget MonCat).mapCocone t) j) x
  uniq t m h := MonoidHom.ext fun y => congr_fun
      ((Types.TypeMax.colimitCoconeIsColimit (F â‹™ forget MonCat)).uniq ((forget MonCat).mapCocone t)
        ((forget MonCat).map m)
        fun j => funext fun x => DFunLike.congr_fun (i := MonCat.instFunLike _ _) (h j) x) y
#align Mon.filtered_colimits.colimit_cocone_is_colimit MonCat.FilteredColimits.colimitCoconeIsColimit
#align AddMon.filtered_colimits.colimit_cocone_is_colimit AddMonCat.FilteredColimits.colimitCoconeIsColimit

@[to_additive]
noncomputable instance forgetPreservesFilteredColimits :
    PreservesFilteredColimits (forget MonCat.{u}) :=
  âŸ¨fun J hJ1 _ => letI hJ1' : Category J := hJ1
    âŸ¨fun {F} => preservesColimitOfPreservesColimitCocone (colimitCoconeIsColimit.{u, u} F)
      (Types.TypeMax.colimitCoconeIsColimit (F â‹™ forget MonCat.{u}))âŸ©âŸ©
end

end MonCat.FilteredColimits

namespace CommMonCat.FilteredColimits

open MonCat.FilteredColimits (colimit_mul_mk_eq)

section

-- We use parameters here, mainly so we can have the abbreviation `M` below, without
-- passing around `F` all the time.
variable {J : Type v} [SmallCategory J] [IsFiltered J] (F : J â¥¤ CommMonCat.{max v u})

/-- The colimit of `F â‹™ forgetâ‚‚ CommMonCat MonCat` in the category `MonCat`.
In the following, we will show that this has the structure of a _commutative_ monoid.
-/
@[to_additive
      "The colimit of `F â‹™ forgetâ‚‚ AddCommMonCat AddMonCat` in the category `AddMonCat`. In the
      following, we will show that this has the structure of a _commutative_ additive monoid."]
noncomputable abbrev M : MonCat.{max v u} :=
  MonCat.FilteredColimits.colimit.{v, u} (F â‹™ forgetâ‚‚ CommMonCat MonCat.{max v u})
#align CommMon.filtered_colimits.M CommMonCat.FilteredColimits.M
#align AddCommMon.filtered_colimits.M AddCommMonCat.FilteredColimits.M

@[to_additive]
noncomputable instance colimitCommMonoid : CommMonoid.{max v u} (M.{v, u} F) :=
  { (M.{v, u} F) with
    mul_comm := fun x y => by
      refine Quot.induction_onâ‚‚ x y ?_
      clear x y
      intro x y
      let k := max' x.1 y.1
      let f := IsFiltered.leftToMax x.1 y.1
      let g := IsFiltered.rightToMax x.1 y.1
      rw [colimit_mul_mk_eq.{v, u} (F â‹™ forgetâ‚‚ CommMonCat MonCat) x y k f g,
        colimit_mul_mk_eq.{v, u} (F â‹™ forgetâ‚‚ CommMonCat MonCat) y x k g f]
      dsimp
      rw [mul_comm] }
#align CommMon.filtered_colimits.colimit_comm_monoid CommMonCat.FilteredColimits.colimitCommMonoid
#align AddCommMon.filtered_colimits.colimit_add_comm_monoid AddCommMonCat.FilteredColimits.colimitAddCommMonoid

/-- The bundled commutative monoid giving the filtered colimit of a diagram. -/
@[to_additive "The bundled additive commutative monoid giving the filtered colimit of a diagram."]
noncomputable def colimit : CommMonCat.{max v u} :=
  CommMonCat.of (M.{v, u} F)
#align CommMon.filtered_colimits.colimit CommMonCat.FilteredColimits.colimit
#align AddCommMon.filtered_colimits.colimit AddCommMonCat.FilteredColimits.colimit

/-- The cocone over the proposed colimit commutative monoid. -/
@[to_additive "The cocone over the proposed colimit additive commutative monoid."]
noncomputable def colimitCocone : Cocone F where
  pt := colimit.{v, u} F
  Î¹ := { (MonCat.FilteredColimits.colimitCocone.{v, u}
    (F â‹™ forgetâ‚‚ CommMonCat MonCat.{max v u})).Î¹ with }
#align CommMon.filtered_colimits.colimit_cocone CommMonCat.FilteredColimits.colimitCocone
#align AddCommMon.filtered_colimits.colimit_cocone AddCommMonCat.FilteredColimits.colimitCocone

/-- The proposed colimit cocone is a colimit in `CommMonCat`. -/
@[to_additive "The proposed colimit cocone is a colimit in `AddCommMonCat`."]
def colimitCoconeIsColimit : IsColimit (colimitCocone.{v, u} F) where
  desc t :=
    MonCat.FilteredColimits.colimitDesc.{v, u} (F â‹™ forgetâ‚‚ CommMonCat MonCat.{max v u})
      ((forgetâ‚‚ CommMonCat MonCat.{max v u}).mapCocone t)
  fac t j :=
    DFunLike.coe_injective (i := CommMonCat.instFunLike _ _) <|
      (Types.TypeMax.colimitCoconeIsColimit.{v, u} (F â‹™ forget CommMonCat.{max v u})).fac
        ((forget CommMonCat).mapCocone t) j
  uniq t m h :=
    DFunLike.coe_injective (i := CommMonCat.instFunLike _ _) <|
      (Types.TypeMax.colimitCoconeIsColimit.{v, u} (F â‹™ forget CommMonCat.{max v u})).uniq
        ((forget CommMonCat.{max v u}).mapCocone t)
        ((forget CommMonCat.{max v u}).map m) fun j => funext fun x =>
          DFunLike.congr_fun (i := CommMonCat.instFunLike _ _) (h j) x
#align CommMon.filtered_colimits.colimit_cocone_is_colimit CommMonCat.FilteredColimits.colimitCoconeIsColimit
#align AddCommMon.filtered_colimits.colimit_cocone_is_colimit AddCommMonCat.FilteredColimits.colimitCoconeIsColimit

@[to_additive forgetâ‚‚AddMonPreservesFilteredColimits]
noncomputable instance forgetâ‚‚MonPreservesFilteredColimits :
  PreservesFilteredColimits (forgetâ‚‚ CommMonCat MonCat.{u}) :=
âŸ¨fun J hJ1 _ => letI hJ3 : Category J := hJ1
  âŸ¨fun {F} => preservesColimitOfPreservesColimitCocone (colimitCoconeIsColimit.{u, u} F)
    (MonCat.FilteredColimits.colimitCoconeIsColimit (F â‹™ forgetâ‚‚ CommMonCat MonCat.{u}))âŸ©âŸ©
#align CommMon.filtered_colimits.forgetâ‚‚_Mon_preserves_filtered_colimits CommMonCat.FilteredColimits.forgetâ‚‚MonPreservesFilteredColimits
#align AddCommMon.filtered_colimits.forgetâ‚‚_AddMon_preserves_filtered_colimits AddCommMonCat.FilteredColimits.forgetâ‚‚AddMonPreservesFilteredColimits

@[to_additive]
noncomputable instance forgetPreservesFilteredColimits :
    PreservesFilteredColimits (forget CommMonCat.{u}) :=
  Limits.compPreservesFilteredColimits (forgetâ‚‚ CommMonCat MonCat) (forget MonCat)
#align CommMon.filtered_colimits.forget_preserves_filtered_colimits CommMonCat.FilteredColimits.forgetPreservesFilteredColimits
#align AddCommMon.filtered_colimits.forget_preserves_filtered_colimits AddCommMonCat.FilteredColimits.forgetPreservesFilteredColimits

end

end CommMonCat.FilteredColimits
