/-
Copyright (c) 2021 Justus Springer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Justus Springer
-/
import Mathlib.CategoryTheory.Limits.Preserves.Filtered
import Mathlib.CategoryTheory.ConcreteCategory.Elementwise
import Mathlib.CategoryTheory.Limits.Types.Filtered
import Mathlib.Algebra.Category.MonCat.Basic

/-!
# The forgetful functor from (commutative) (additive) monoids preserves filtered colimits.

Forgetful functors from algebraic categories usually don't preserve colimits. However, they tend
to preserve _filtered_ colimits.

In this file, we start with a small filtered category `J` and a functor `F : J ‚•§ MonCat`.
We then construct a monoid structure on the colimit of `F ‚ãô forget MonCat` (in `Type`), thereby
showing that the forgetful functor `forget MonCat` preserves filtered colimits. Similarly for
`AddMonCat`, `CommMonCat` and `AddCommMonCat`.

-/


universe v u

noncomputable section

open CategoryTheory Limits

open IsFiltered renaming max ‚Üí max' -- avoid name collision with `_root_.max`.

namespace MonCat.FilteredColimits

section

-- Porting note: mathlib 3 used `parameters` here, mainly so we can have the abbreviations `M` and
-- `M.mk` below, without passing around `F` all the time.
variable {J : Type v} [SmallCategory J] (F : J ‚•§ MonCat.{max v u})

/-- The colimit of `F ‚ãô forget MonCat` in the category of types.
In the following, we will construct a monoid structure on `M`.
-/
@[to_additive
      /-- The colimit of `F ‚ãô forget AddMon` in the category of types.
      In the following, we will construct an additive monoid structure on `M`. -/]
abbrev M := (F ‚ãô forget MonCat).ColimitType

/-- The canonical projection into the colimit, as a quotient type. -/
@[to_additive /-- The canonical projection into the colimit, as a quotient type. -/]
noncomputable abbrev M.mk : (Œ£ j, F.obj j) ‚Üí M.{v, u} F :=
  fun x ‚Ü¶ (F ‚ãô forget MonCat).ŒπColimitType x.1 x.2

@[to_additive]
lemma M.mk_surjective (m : M.{v, u} F) :
    ‚àÉ (j : J) (x : F.obj j), M.mk F ‚ü®j, x‚ü© = m :=
  (F ‚ãô forget MonCat).ŒπColimitType_jointly_surjective m

@[to_additive]
theorem M.mk_eq (x y : Œ£ j, F.obj j)
    (h : ‚àÉ (k : J) (f : x.1 ‚ü∂ k) (g : y.1 ‚ü∂ k), F.map f x.2 = F.map g y.2) :
    M.mk.{v, u} F x = M.mk F y :=
  Quot.eqvGen_sound (Types.FilteredColimit.eqvGen_colimitTypeRel_of_rel (F ‚ãô forget MonCat) x y h)

@[to_additive]
lemma M.map_mk {j k : J} (f : j ‚ü∂ k) (x : F.obj j) :
    M.mk F ‚ü®k, F.map f x‚ü© = M.mk F ‚ü®j, x‚ü© :=
  M.mk_eq _ _ _ ‚ü®k, ùüô _, f, by simp‚ü©

variable [IsFiltered J]

/-- As `J` is nonempty, we can pick an arbitrary object `j‚ÇÄ : J`. We use this object to define the
"one" in the colimit as the equivalence class of `‚ü®j‚ÇÄ, 1 : F.obj j‚ÇÄ‚ü©`.
-/
@[to_additive
  /-- As `J` is nonempty, we can pick an arbitrary object `j‚ÇÄ : J`. We use this object to
  define the "zero" in the colimit as the equivalence class of `‚ü®j‚ÇÄ, 0 : F.obj j‚ÇÄ‚ü©`. -/]
noncomputable instance colimitOne : One (M.{v, u} F) where
  one := M.mk F ‚ü®IsFiltered.nonempty.some,1‚ü©

/-- The definition of the "one" in the colimit is independent of the chosen object of `J`.
In particular, this lemma allows us to "unfold" the definition of `colimit_one` at a custom chosen
object `j`.
-/
@[to_additive
      /-- The definition of the "zero" in the colimit is independent of the chosen object
      of `J`. In particular, this lemma allows us to "unfold" the definition of `colimit_zero` at
      a custom chosen object `j`. -/]
theorem colimit_one_eq (j : J) : (1 : M.{v, u} F) = M.mk F ‚ü®j, 1‚ü© := by
  apply M.mk_eq
  refine ‚ü®max' _ j, IsFiltered.leftToMax _ j, IsFiltered.rightToMax _ j, ?_‚ü©
  simp

/-- The "unlifted" version of multiplication in the colimit. To multiply two dependent pairs
`‚ü®j‚ÇÅ, x‚ü©` and `‚ü®j‚ÇÇ, y‚ü©`, we pass to a common successor of `j‚ÇÅ` and `j‚ÇÇ` (given by `IsFiltered.max`)
and multiply them there.
-/
@[to_additive
      /-- The "unlifted" version of addition in the colimit. To add two dependent pairs
      `‚ü®j‚ÇÅ, x‚ü©` and `‚ü®j‚ÇÇ, y‚ü©`, we pass to a common successor of `j‚ÇÅ` and `j‚ÇÇ`
      (given by `IsFiltered.max`) and add them there. -/]
noncomputable def colimitMulAux (x y : Œ£ j, F.obj j) : M.{v, u} F :=
  M.mk F ‚ü®IsFiltered.max x.fst y.fst, F.map (IsFiltered.leftToMax x.1 y.1) x.2 *
    F.map (IsFiltered.rightToMax x.1 y.1) y.2‚ü©

/-- Multiplication in the colimit is well-defined in the left argument. -/
@[to_additive /-- Addition in the colimit is well-defined in the left argument. -/]
theorem colimitMulAux_eq_of_rel_left {x x' y : Œ£ j, F.obj j}
    (hxx' : Types.FilteredColimit.Rel (F ‚ãô forget MonCat) x x') :
    colimitMulAux.{v, u} F x y = colimitMulAux.{v, u} F x' y := by
  obtain ‚ü®j‚ÇÅ, x‚ü© := x; obtain ‚ü®j‚ÇÇ, y‚ü© := y; obtain ‚ü®j‚ÇÉ, x'‚ü© := x'
  obtain ‚ü®l, f, g, hfg‚ü© := hxx'
  simp? at hfg says
    simp only [Functor.comp_obj, Functor.comp_map, ConcreteCategory.forget_map_eq_coe] at hfg
  obtain ‚ü®s, Œ±, Œ≤, Œ≥, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü© :=
    IsFiltered.tulip (IsFiltered.leftToMax j‚ÇÅ j‚ÇÇ) (IsFiltered.rightToMax j‚ÇÅ j‚ÇÇ)
      (IsFiltered.rightToMax j‚ÇÉ j‚ÇÇ) (IsFiltered.leftToMax j‚ÇÉ j‚ÇÇ) f g
  apply M.mk_eq
  use s, Œ±, Œ≥
  simp_rw [MonoidHom.map_mul, ‚Üê ConcreteCategory.comp_apply, ‚Üê F.map_comp, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ, F.map_comp,
    ConcreteCategory.comp_apply, hfg]

/-- Multiplication in the colimit is well-defined in the right argument. -/
@[to_additive /-- Addition in the colimit is well-defined in the right argument. -/]
theorem colimitMulAux_eq_of_rel_right {x y y' : Œ£ j, F.obj j}
    (hyy' : Types.FilteredColimit.Rel (F ‚ãô forget MonCat) y y') :
    colimitMulAux.{v, u} F x y = colimitMulAux.{v, u} F x y' := by
  obtain ‚ü®j‚ÇÅ, y‚ü© := y; obtain ‚ü®j‚ÇÇ, x‚ü© := x; obtain ‚ü®j‚ÇÉ, y'‚ü© := y'
  obtain ‚ü®l, f, g, hfg‚ü© := hyy'
  simp only [Functor.comp_obj, Functor.comp_map, forget_map] at hfg
  obtain ‚ü®s, Œ±, Œ≤, Œ≥, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü© :=
    IsFiltered.tulip (IsFiltered.rightToMax j‚ÇÇ j‚ÇÅ) (IsFiltered.leftToMax j‚ÇÇ j‚ÇÅ)
      (IsFiltered.leftToMax j‚ÇÇ j‚ÇÉ) (IsFiltered.rightToMax j‚ÇÇ j‚ÇÉ) f g
  apply M.mk_eq
  use s, Œ±, Œ≥
  dsimp
  simp_rw [MonoidHom.map_mul, ‚Üê ConcreteCategory.comp_apply, ‚Üê F.map_comp, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ, F.map_comp,
    ConcreteCategory.comp_apply, hfg]

/-- Multiplication in the colimit. See also `colimitMulAux`. -/
@[to_additive /-- Addition in the colimit. See also `colimitAddAux`. -/]
noncomputable instance colimitMul : Mul (M.{v, u} F) :=
{ mul := fun x y => by
    refine Quot.lift‚ÇÇ (colimitMulAux F) ?_ ?_ x y
    ¬∑ intro x y y' h
      apply colimitMulAux_eq_of_rel_right
      apply Types.FilteredColimit.rel_of_colimitTypeRel
      exact h
    ¬∑ intro x x' y h
      apply colimitMulAux_eq_of_rel_left
      apply Types.FilteredColimit.rel_of_colimitTypeRel
      exact h }

/-- Multiplication in the colimit is independent of the chosen "maximum" in the filtered category.
In particular, this lemma allows us to "unfold" the definition of the multiplication of `x` and `y`,
using a custom object `k` and morphisms `f : x.1 ‚ü∂ k` and `g : y.1 ‚ü∂ k`.
-/
@[to_additive
      /-- Addition in the colimit is independent of the chosen "maximum" in the filtered
      category. In particular, this lemma allows us to "unfold" the definition of the addition of
      `x` and `y`, using a custom object `k` and morphisms `f : x.1 ‚ü∂ k` and `g : y.1 ‚ü∂ k`. -/]
theorem colimit_mul_mk_eq (x y : Œ£ j, F.obj j) (k : J) (f : x.1 ‚ü∂ k) (g : y.1 ‚ü∂ k) :
    M.mk.{v, u} F x * M.mk F y = M.mk F ‚ü®k, F.map f x.2 * F.map g y.2‚ü© := by
  obtain ‚ü®j‚ÇÅ, x‚ü© := x; obtain ‚ü®j‚ÇÇ, y‚ü© := y
  obtain ‚ü®s, Œ±, Œ≤, h‚ÇÅ, h‚ÇÇ‚ü© := IsFiltered.bowtie (IsFiltered.leftToMax j‚ÇÅ j‚ÇÇ) f
    (IsFiltered.rightToMax j‚ÇÅ j‚ÇÇ) g
  refine M.mk_eq F _ _ ?_
  use s, Œ±, Œ≤
  dsimp
  simp_rw [MonoidHom.map_mul, ‚Üê ConcreteCategory.comp_apply, ‚Üê F.map_comp, h‚ÇÅ, h‚ÇÇ]

@[to_additive]
lemma colimit_mul_mk_eq' {j : J} (x y : F.obj j) :
    M.mk.{v, u} F ‚ü®j, x‚ü© * M.mk.{v, u} F ‚ü®j, y‚ü© = M.mk.{v, u} F ‚ü®j, x * y‚ü© := by
  simpa using colimit_mul_mk_eq F ‚ü®j, x‚ü© ‚ü®j, y‚ü© j (ùüô _) (ùüô _)

@[to_additive]
noncomputable instance colimitMulOneClass : MulOneClass (M.{v, u} F) :=
  { colimitOne F,
    colimitMul F with
    one_mul := fun x => by
      obtain ‚ü®j, x, rfl‚ü© := x.mk_surjective
      rw [colimit_one_eq F j, colimit_mul_mk_eq', one_mul]
    mul_one := fun x => by
      obtain ‚ü®j, x, rfl‚ü© := x.mk_surjective
      rw [colimit_one_eq F j, colimit_mul_mk_eq', mul_one] }

@[to_additive]
noncomputable instance colimitMonoid : Monoid (M.{v, u} F) :=
  { colimitMulOneClass F with
    mul_assoc := fun x y z => by
      obtain ‚ü®j‚ÇÅ, x‚ÇÅ, rfl‚ü© := x.mk_surjective
      obtain ‚ü®j‚ÇÇ, y‚ÇÇ, rfl‚ü© := y.mk_surjective
      obtain ‚ü®j‚ÇÉ, z‚ÇÉ, rfl‚ü© := z.mk_surjective
      obtain ‚ü®j, f‚ÇÅ, f‚ÇÇ, f‚ÇÉ, x, y, z, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü© :
          ‚àÉ (j : J) (f‚ÇÅ : j‚ÇÅ ‚ü∂ j) (f‚ÇÇ : j‚ÇÇ ‚ü∂ j) (f‚ÇÉ : j‚ÇÉ ‚ü∂ j) (x y z : F.obj j),
          F.map f‚ÇÅ x‚ÇÅ = x ‚àß F.map f‚ÇÇ y‚ÇÇ = y ‚àß F.map f‚ÇÉ z‚ÇÉ = z :=
        ‚ü®IsFiltered.max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ, IsFiltered.firstToMax‚ÇÉ _ _ _,
          IsFiltered.secondToMax‚ÇÉ _ _ _, IsFiltered.thirdToMax‚ÇÉ _ _ _,
          _, _, _, rfl, rfl, rfl‚ü©
      simp only [‚Üê M.map_mk F f‚ÇÅ, ‚Üê M.map_mk F f‚ÇÇ, ‚Üê M.map_mk F f‚ÇÉ, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ,
        colimit_mul_mk_eq', mul_assoc] }

/-- The bundled monoid giving the filtered colimit of a diagram. -/
@[to_additive
  /-- The bundled additive monoid giving the filtered colimit of a diagram. -/]
noncomputable def colimit : MonCat.{max v u} :=
  MonCat.of (M.{v, u} F)

/-- The monoid homomorphism from a given monoid in the diagram to the colimit monoid. -/
@[to_additive
      /-- The additive monoid homomorphism from a given additive monoid in the diagram to the
      colimit additive monoid. -/]
noncomputable def coconeMorphism (j : J) : F.obj j ‚ü∂ colimit F :=
  ofHom
  { toFun := (Types.TypeMax.colimitCocone.{v, max v u, v} (F ‚ãô forget MonCat)).Œπ.app j
    map_one' := (colimit_one_eq F j).symm
    map_mul' x y := by symm; apply colimit_mul_mk_eq' }

@[to_additive (attr := simp)]
theorem cocone_naturality {j j' : J} (f : j ‚ü∂ j') :
    F.map f ‚â´ coconeMorphism.{v, u} F j' = coconeMorphism F j :=
  MonCat.ext fun x =>
    congr_fun ((Types.TypeMax.colimitCocone (F ‚ãô forget MonCat)).Œπ.naturality f) x

/-- The cocone over the proposed colimit monoid. -/
@[to_additive /-- The cocone over the proposed colimit additive monoid. -/]
noncomputable def colimitCocone : Cocone F where
  pt := colimit.{v, u} F
  Œπ := { app := coconeMorphism F }

/-- Given a cocone `t` of `F`, the induced monoid homomorphism from the colimit to the cocone point.
As a function, this is simply given by the induced map of the corresponding cocone in `Type`.
The only thing left to see is that it is a monoid homomorphism.
-/
@[to_additive
      /-- Given a cocone `t` of `F`, the induced additive monoid homomorphism from the colimit
      to the cocone point. As a function, this is simply given by the induced map of the
      corresponding cocone in `Type`. The only thing left to see is that it is an additive monoid
      homomorphism. -/]
noncomputable def colimitDesc (t : Cocone F) : colimit.{v, u} F ‚ü∂ t.pt :=
  ofHom
  { toFun := (F ‚ãô forget MonCat).descColimitType
        ((F ‚ãô forget MonCat).coconeTypesEquiv.symm ((forget MonCat).mapCocone t))
    map_one' := by
      rw [colimit_one_eq F IsFiltered.nonempty.some]
      exact MonoidHom.map_one _
    map_mul' x y := by
      obtain ‚ü®i, x, rfl‚ü© := x.mk_surjective
      obtain ‚ü®j, y, rfl‚ü© := y.mk_surjective
      rw [colimit_mul_mk_eq F ‚ü®i, x‚ü© ‚ü®j, y‚ü© (max' i j) (IsFiltered.leftToMax i j)
        (IsFiltered.rightToMax i j)]
      dsimp
      rw [MonoidHom.map_mul, t.w_apply, t.w_apply]
      rfl }

/-- The proposed colimit cocone is a colimit in `MonCat`. -/
@[to_additive /-- The proposed colimit cocone is a colimit in `AddMonCat`. -/]
noncomputable def colimitCoconeIsColimit : IsColimit (colimitCocone.{v, u} F) where
  desc := colimitDesc.{v, u} F
  fac t j := rfl
  uniq t m h := MonCat.ext fun y ‚Ü¶ by
    obtain ‚ü®j, y, rfl‚ü© := Functor.ŒπColimitType_jointly_surjective _ y
    exact ConcreteCategory.congr_hom (h j) y

@[to_additive]
instance forget_preservesFilteredColimits :
    PreservesFilteredColimits (forget MonCat.{u}) where
  preserves_filtered_colimits _ _ _ :=
    ‚ü®fun {F} => preservesColimit_of_preserves_colimit_cocone (colimitCoconeIsColimit.{u, u} F)
      (Types.TypeMax.colimitCoconeIsColimit (F ‚ãô forget MonCat.{u}))‚ü©
end

end MonCat.FilteredColimits

namespace CommMonCat.FilteredColimits

open MonCat.FilteredColimits (colimit_mul_mk_eq)

section

-- We use parameters here, mainly so we can have the abbreviation `M` below, without
-- passing around `F` all the time.
variable {J : Type v} [SmallCategory J] [IsFiltered J] (F : J ‚•§ CommMonCat.{max v u})

/-- The colimit of `F ‚ãô forget‚ÇÇ CommMonCat MonCat` in the category `MonCat`.
In the following, we will show that this has the structure of a _commutative_ monoid.
-/
@[to_additive
      /-- The colimit of `F ‚ãô forget‚ÇÇ AddCommMonCat AddMonCat` in the category `AddMonCat`. In the
      following, we will show that this has the structure of a _commutative_ additive monoid. -/]
noncomputable abbrev M : MonCat.{max v u} :=
  MonCat.FilteredColimits.colimit.{v, u} (F ‚ãô forget‚ÇÇ CommMonCat MonCat.{max v u})

@[to_additive]
noncomputable instance colimitCommMonoid : CommMonoid.{max v u} (M.{v, u} F) :=
  { (M.{v, u} F) with
    mul_comm := fun x y => by
      obtain ‚ü®i, x, rfl‚ü© := x.mk_surjective
      obtain ‚ü®j, y, rfl‚ü© := y.mk_surjective
      let k := max' i j
      let f := IsFiltered.leftToMax i j
      let g := IsFiltered.rightToMax i j
      rw [colimit_mul_mk_eq.{v, u} (F ‚ãô forget‚ÇÇ CommMonCat MonCat) ‚ü®i, x‚ü© ‚ü®j, y‚ü© k f g,
        colimit_mul_mk_eq.{v, u} (F ‚ãô forget‚ÇÇ CommMonCat MonCat) ‚ü®j, y‚ü© ‚ü®i, x‚ü© k g f]
      dsimp
      rw [mul_comm] }

/-- The bundled commutative monoid giving the filtered colimit of a diagram. -/
@[to_additive
/-- The bundled additive commutative monoid giving the filtered colimit of a diagram. -/]
noncomputable def colimit : CommMonCat.{max v u} :=
  CommMonCat.of (M.{v, u} F)

/-- The cocone over the proposed colimit commutative monoid. -/
@[to_additive /-- The cocone over the proposed colimit additive commutative monoid. -/]
noncomputable def colimitCocone : Cocone F where
  pt := colimit.{v, u} F
  Œπ.app j := ofHom ((MonCat.FilteredColimits.colimitCocone.{v, u}
    (F ‚ãô forget‚ÇÇ CommMonCat MonCat.{max v u})).Œπ.app j).hom
  Œπ.naturality _ _ f := hom_ext <| congr_arg (MonCat.Hom.hom)
    ((MonCat.FilteredColimits.colimitCocone.{v, u}
      (F ‚ãô forget‚ÇÇ CommMonCat MonCat.{max v u})).Œπ.naturality f)

/-- The proposed colimit cocone is a colimit in `CommMonCat`. -/
@[to_additive /-- The proposed colimit cocone is a colimit in `AddCommMonCat`. -/]
noncomputable def colimitCoconeIsColimit : IsColimit (colimitCocone.{v, u} F) :=
  isColimitOfReflects (forget‚ÇÇ CommMonCat MonCat)
    (MonCat.FilteredColimits.colimitCoconeIsColimit (F ‚ãô forget‚ÇÇ _ _))

@[to_additive forget‚ÇÇAddMonPreservesFilteredColimits]
noncomputable instance forget‚ÇÇMon_preservesFilteredColimits :
    PreservesFilteredColimits (forget‚ÇÇ CommMonCat MonCat.{u}) where
  preserves_filtered_colimits _ _ _ :=
    ‚ü®fun {F} => preservesColimit_of_preserves_colimit_cocone (colimitCoconeIsColimit.{u, u} F)
      (MonCat.FilteredColimits.colimitCoconeIsColimit (F ‚ãô forget‚ÇÇ CommMonCat MonCat.{u}))‚ü©

@[to_additive]
noncomputable instance forget_preservesFilteredColimits :
    PreservesFilteredColimits (forget CommMonCat.{u}) :=
  Limits.comp_preservesFilteredColimits (forget‚ÇÇ CommMonCat MonCat) (forget MonCat)

end

end CommMonCat.FilteredColimits
