/-
Copyright (c) 2025 Ya√´l Dillies, Christian Merten, Micha≈Ç Mruga≈Ça, Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Christian Merten, Micha≈Ç Mruga≈Ça, Andrew Yang
-/
import Mathlib.Algebra.Category.AlgebraCat.Basic
import Mathlib.Algebra.Category.Ring.Under.Basic
import Mathlib.CategoryTheory.ChosenFiniteProducts

/-!
# Category of commutative algebras over a commutative ring

We introduce the bundled category `CommAlg` of algebras over a fixed commutative ring `R` along
with the forgetful functors to `RingCat` and `ModuleCat`. We furthermore show that the functor
associating to a type the free `R`-algebra on that type is left adjoint to the forgetful functor.

## Implementation notes

`CommAlg R` is the same as `Under R` up to two details:
* `A : CommAlg R` contains the data of both `algebraMap R A : R ‚Üí A` and
  `Algebra.smul : R ‚Üí A ‚Üí A`. `A : Under R` only contains `algebraMap R A`, meaning that going
  from an unbundled algebra to `Under R` and back to an unbundled algebra gives a propeq but not
  defeq algebra. In comparison, going from an unbundled algebra to `CommAlg R` and back to an
  unbundled algebra gives a defeq algebra.
* If `A : Under R`, then `A` must live in the same universe as `R`. This is not terribly important,
  because if `R : Type u` then `CommAlg.{v} R` is cartesian-monoidal only if `u ‚â§ v` and,
  for convenience, we only provide the `ChosenFiniteProducts (CommAlg.{u} R)` instance.
-/

open CategoryTheory Limits

universe v u

variable {R : Type u} [CommRing R]

variable (R) in
/-- The category of R-algebras and their morphisms. -/
structure CommAlg where
  private mk ::
  /-- The underlying type. -/
  carrier : Type v
  [isCommRing : CommRing carrier]
  [isAlgebra : Algebra R carrier]

attribute [instance] CommAlg.isCommRing CommAlg.isAlgebra

initialize_simps_projections CommAlg (-isCommRing, -isAlgebra)

namespace CommAlg
variable {A B C : CommAlg.{v} R}

instance : CoeSort (CommAlg R) (Type v) := ‚ü®CommAlg.carrier‚ü©

attribute [coe] CommAlg.carrier

variable (R) in
/-- The object in the category of R-algebras associated to a type equipped with the appropriate
typeclasses. This is the preferred way to construct a term of `CommAlg R`. -/
abbrev of (X : Type v) [CommRing X] [Algebra R X] : CommAlg.{v} R := ‚ü®X‚ü©

variable (R) in
lemma coe_of (X : Type v) [CommRing X] [Algebra R X] : (of R X : Type v) = X := rfl

/-- The type of morphisms in `CommAlg R`. -/
@[ext]
structure Hom (A B : CommAlg.{v} R) where
  private mk ::
  /-- The underlying algebra map. -/
  hom' : A ‚Üí‚Çê[R] B

instance : Category (CommAlg.{v} R) where
  Hom A B := Hom A B
  id A := ‚ü®AlgHom.id R A‚ü©
  comp f g := ‚ü®g.hom'.comp f.hom'‚ü©

instance : ConcreteCategory (CommAlg.{v} R) (¬∑ ‚Üí‚Çê[R] ¬∑) where
  hom := Hom.hom'
  ofHom := Hom.mk

/-- Turn a morphism in `CommAlg` back into an `AlgHom`. -/
abbrev Hom.hom {A B : CommAlg.{v} R} (f : Hom A B) :=
  ConcreteCategory.hom (C := CommAlg R) f

/-- Typecheck an `AlgHom` as a morphism in `CommAlg`. -/
abbrev ofHom {A B : Type v} [CommRing A] [CommRing B] [Algebra R A] [Algebra R B] (f : A ‚Üí‚Çê[R] B) :
    of R A ‚ü∂ of R B :=
  ConcreteCategory.ofHom (C := CommAlg R) f

/-- Use the `ConcreteCategory.hom` projection for `@[simps]` lemmas. -/
def Hom.Simps.hom (A B : CommAlg.{v} R) (f : Hom A B) := f.hom

initialize_simps_projections Hom (hom' ‚Üí hom)

/-!
The results below duplicate the `ConcreteCategory` simp lemmas, but we can keep them for `dsimp`.
-/

@[simp] lemma hom_id {A : CommAlg.{v} R} : (ùüô A : A ‚ü∂ A).hom = AlgHom.id R A := rfl

/- Provided for rewriting. -/
lemma id_apply (A : CommAlg.{v} R) (a : A) : (ùüô A : A ‚ü∂ A) a = a := by simp

@[simp] lemma hom_comp (f : A ‚ü∂ B) (g : B ‚ü∂ C) : (f ‚â´ g).hom = g.hom.comp f.hom := rfl

/- Provided for rewriting. -/
lemma comp_apply (f : A ‚ü∂ B) (g : B ‚ü∂ C) (a : A) : (f ‚â´ g) a = g (f a) := by simp

@[ext] lemma hom_ext {f g : A ‚ü∂ B} (hf : f.hom = g.hom) : f = g := Hom.ext hf

@[simp]
lemma hom_ofHom {X Y : Type v} [CommRing X] [Algebra R X] [CommRing Y] [Algebra R Y]
    (f : X ‚Üí‚Çê[R] Y) : (ofHom f).hom = f := rfl

@[simp] lemma ofHom_hom (f : A ‚ü∂ B) : ofHom (Hom.hom f) = f := rfl

@[simp]
lemma ofHom_id {X : Type v} [CommRing X] [Algebra R X] : ofHom (AlgHom.id R X) = ùüô (of R X) := rfl

@[simp]
lemma ofHom_comp {X Y Z : Type v} [CommRing X] [CommRing Y] [CommRing Z] [Algebra R X] [Algebra R Y]
    [Algebra R Z] (f : X ‚Üí‚Çê[R] Y) (g : Y ‚Üí‚Çê[R] Z) :
    ofHom (g.comp f) = ofHom f ‚â´ ofHom g := rfl

lemma ofHom_apply {X Y : Type v} [CommRing X] [Algebra R X] [CommRing Y] [Algebra R Y]
    (f : X ‚Üí‚Çê[R] Y) (x : X) : ofHom f x = f x := rfl

lemma inv_hom_apply (e : A ‚âÖ B) (x : A) : e.inv (e.hom x) = x := by simp [‚Üê comp_apply]

lemma hom_inv_apply (e : A ‚âÖ B) (x : B) : e.hom (e.inv x) = x := by simp [‚Üê comp_apply]

instance : Inhabited (CommAlg R) := ‚ü®of R R‚ü©

lemma forget_obj (A : CommAlg.{v} R) : (forget (CommAlg.{v} R)).obj A = A := rfl

lemma forget_map (f : A ‚ü∂ B) : (forget (CommAlg.{v} R)).map f = f := rfl

instance {S : CommAlg.{v} R} : Ring ((forget (CommAlg R)).obj S) :=
  inferInstanceAs <| Ring S.carrier

instance {S : CommAlg.{v} R} : Algebra R ((forget (CommAlg R)).obj S) :=
  inferInstanceAs <| Algebra R S.carrier

instance hasForgetToCommRing : HasForget‚ÇÇ (CommAlg.{v} R) CommRingCat.{v} where
  forget‚ÇÇ.obj A := CommRingCat.of A
  forget‚ÇÇ.map f := CommRingCat.ofHom f.hom.toRingHom

instance hasForgetToAlgebra : HasForget‚ÇÇ (CommAlg.{v} R) (AlgebraCat.{v} R) where
  forget‚ÇÇ.obj M := .of R M
  forget‚ÇÇ.map f := AlgebraCat.ofHom f.hom

@[simp]
lemma forget‚ÇÇ_Algebra_obj (X : CommAlg.{v} R) :
    (forget‚ÇÇ (CommAlg.{v} R) (AlgebraCat.{v} R)).obj X = .of R X := rfl

@[simp]
lemma forget‚ÇÇ_Algebra_map {X Y : CommAlg.{v} R} (f : X ‚ü∂ Y) :
    (forget‚ÇÇ (CommAlg.{v} R) (AlgebraCat.{v} R)).map f = AlgebraCat.ofHom f.hom := rfl

/-- Forgetting to the underlying type and then building the bundled object returns the original
algebra. -/
@[simps]
def ofSelfIso (M : CommAlg.{v} R) : CommAlg.of R M ‚âÖ M where
  hom := ùüô M
  inv := ùüô M

end CommAlg

variable {X‚ÇÅ X‚ÇÇ : Type u}

/-- Build an isomorphism in the category `CommAlg R` from a `AlgEquiv` between `Algebra`s. -/
@[simps]
def AlgEquiv.toCommAlgIso
    {g‚ÇÅ : CommRing X‚ÇÅ} {g‚ÇÇ : CommRing X‚ÇÇ} {m‚ÇÅ : Algebra R X‚ÇÅ} {m‚ÇÇ : Algebra R X‚ÇÇ}
    (e : X‚ÇÅ ‚âÉ‚Çê[R] X‚ÇÇ) : CommAlg.of R X‚ÇÅ ‚âÖ CommAlg.of R X‚ÇÇ where
  hom := CommAlg.ofHom (e : X‚ÇÅ ‚Üí‚Çê[R] X‚ÇÇ)
  inv := CommAlg.ofHom (e.symm : X‚ÇÇ ‚Üí‚Çê[R] X‚ÇÅ)

namespace CategoryTheory.Iso

/-- Build a `AlgEquiv` from an isomorphism in the category `CommAlg R`. -/
@[simps]
def commAlgIsoToAlgEquiv {X Y : CommAlg R} (i : X ‚âÖ Y) : X ‚âÉ‚Çê[R] Y where
  __ := i.hom.hom
  toFun := i.hom
  invFun := i.inv
  left_inv x := by simp
  right_inv x := by simp

end CategoryTheory.Iso

/-- Algebra equivalences between `Algebra`s are the same as isomorphisms in `CommAlg`. -/
@[simps!]
def algEquivEquivCommAlgIso {X Y : Type u} [CommRing X] [CommRing Y] [Algebra R X] [Algebra R Y] :
    (X ‚âÉ‚Çê[R] Y) ‚âÉ (CommAlg.of R X ‚âÖ CommAlg.of R Y) where
  toFun := AlgEquiv.toCommAlgIso
  invFun := Iso.commAlgIsoToAlgEquiv
  left_inv _ := rfl
  right_inv _ := rfl

instance CommAlg.forget_reflects_isos : (forget (CommAlg.{u} R)).ReflectsIsomorphisms where
  reflects {X Y} f _ := by
    let i := asIso ((forget (CommAlg.{u} R)).map f)
    let e : X ‚âÉ‚Çê[R] Y := { f.hom, i.toEquiv with }
    exact e.toCommAlgIso.isIso_hom

namespace CommAlg

noncomputable section Coprod

open TensorProduct

variable (A B C : CommAlg.{v} R)

/-- The explicit cocone with tensor products as the fibered product in `CommRingCat`. -/
def binaryCofan : BinaryCofan A B :=
  .mk (ofHom Algebra.TensorProduct.includeLeft)
    (ofHom (Algebra.TensorProduct.includeRight (R := R) (A := A)))

@[simp]
lemma binaryCofan_inl : (binaryCofan A B).inl = ofHom Algebra.TensorProduct.includeLeft := rfl

@[simp]
lemma binaryCofan_inr : (binaryCofan A B).inr = ofHom Algebra.TensorProduct.includeRight := rfl

@[simp] lemma binaryCofan_pt : (binaryCofan A B).pt = .of R (A ‚äó[R] B) := rfl

/-- Verify that the `pushout_cocone` is indeed the colimit. -/
def binaryCofanIsColimit : IsColimit (binaryCofan A B) :=
  BinaryCofan.IsColimit.mk _
    (fun f g ‚Ü¶ ofHom (Algebra.TensorProduct.lift f.hom g.hom fun _ _ ‚Ü¶ .all _ _))
    (fun f g ‚Ü¶ by ext1; exact Algebra.TensorProduct.lift_comp_includeLeft _ _ fun _ _ ‚Ü¶ .all _ _)
    (fun f g ‚Ü¶ by ext1; exact Algebra.TensorProduct.lift_comp_includeRight _ _ fun _ _ ‚Ü¶ .all _ _)
    (fun f g m hm‚ÇÅ hm‚ÇÇ ‚Ü¶ by
      ext1
      refine Algebra.TensorProduct.liftEquiv.symm_apply_eq (y := ‚ü®‚ü®_, _‚ü©, fun _ _ ‚Ü¶ .all _ _‚ü©).mp ?_
      exact Subtype.ext (Prod.ext congr(($hm‚ÇÅ).hom) congr(($hm‚ÇÇ).hom)))

/-- The initial `R`-algebra is `R`. -/
def isInitialSelf : IsInitial (of R R) := .ofUniqueHom (fun A ‚Ü¶ ofHom (Algebra.ofId R A))
  fun _ _ ‚Ü¶ hom_ext (Algebra.ext_id _ _ _)

open Opposite

instance : ChosenFiniteProducts (CommAlg.{u} R)·µí·µñ where
  product A B := ‚ü®(binaryCofan (unop A) (unop B)).op,
    BinaryCofan.IsColimit.op <| binaryCofanIsColimit (unop A) (unop B)‚ü©
  terminal := ‚ü®_, terminalOpOfInitial isInitialSelf‚ü©

open MonoidalCategory

variable {A B C D : CommAlg.{u} R}

lemma rightWhisker_hom (f : A ‚ü∂ B) (C : CommAlg.{u} R) :
    (f.op ‚ñ∑ op C).unop.hom = Algebra.TensorProduct.map f.hom (.id _ _) := by
  suffices f.op ‚ñ∑ op C = (CommAlg.ofHom (Algebra.TensorProduct.map f.hom (.id _ _))).op by
    rw [this]; rfl
  ext
  ¬∑ simp
    rfl
  simp only [ChosenFiniteProducts.whiskerRight_snd]
  apply Quiver.Hom.unop_inj
  ext x
  show 1 ‚äó‚Çú[R] x = f 1 ‚äó‚Çú[R] x
  simp

lemma leftWhisker_hom (C : CommAlg.{u} R) (f : A ‚ü∂ B) :
    (op C ‚óÅ f.op).unop.hom = Algebra.TensorProduct.map (.id _ _) f.hom := by
  suffices op C ‚óÅ f.op = (CommAlg.ofHom (Algebra.TensorProduct.map (.id _ _) f.hom)).op by
    rw [this]; rfl
  ext
  swap
  ¬∑ simp
    rfl
  simp only [ChosenFiniteProducts.whiskerLeft_fst]
  apply Quiver.Hom.unop_inj
  ext x
  show x ‚äó‚Çú[R] 1 = x ‚äó‚Çú[R] f 1
  simp

lemma associator_hom_unop_hom (A B C : CommAlg.{u} R) :
    (Œ±_ (op A) (op B) (op C)).hom.unop.hom =
      (Algebra.TensorProduct.assoc R A B C).symm.toAlgHom := by
  suffices (Œ±_ (op A) (op B) (op C)).hom =
      (CommAlg.ofHom (Algebra.TensorProduct.assoc R A B C).symm.toAlgHom).op by
    rw [this]; rfl
  ext <;> simp <;> rfl

lemma associator_inv_unop_hom (A B C : CommAlg.{u} R) :
    (Œ±_ (op A) (op B) (op C)).inv.unop.hom = (Algebra.TensorProduct.assoc R A B C).toAlgHom := by
  suffices (Œ±_ (op A) (op B) (op C)).inv =
      (CommAlg.ofHom (Algebra.TensorProduct.assoc R A B C).toAlgHom).op by
    rw [this]; rfl
  ext <;> simp <;> rfl

lemma tensorHom_unop_hom (f : A ‚ü∂ C) (g : B ‚ü∂ D) :
    (MonoidalCategoryStruct.tensorHom f.op g.op).unop.hom =
      (Algebra.TensorProduct.map f.hom g.hom) := by
  rw [MonoidalCategory.tensorHom_def]
  ext
  simp only [unop_comp, CommAlg.hom_comp, CommAlg.rightWhisker_hom, CommAlg.hom_ofHom,
    CommAlg.leftWhisker_hom]
  rw [‚Üê Algebra.TensorProduct.map_comp]
  simp

end Coprod

end CommAlg

/-- The category of commutative algebras over a commutative ring `R` is the same as rings under `R`.
-/
@[simps]
def commAlgEquivUnder (R : CommRingCat.{u}) : CommAlg.{u} R ‚âå Under.{u} R where
  functor.obj A := R.mkUnder A
  functor.map {A B} f := f.hom.toUnder
  inverse.obj A := CommAlg.of _ A
  inverse.map {A B} f := CommAlg.ofHom <| CommRingCat.toAlgHom f
  unitIso := NatIso.ofComponents fun A ‚Ü¶
    AlgEquiv.toCommAlgIso { __ := RingEquiv.refl A, commutes' _ := rfl }
  counitIso := .refl _
