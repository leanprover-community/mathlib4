/-
Copyright (c) 2024 Amelia Livingston. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Amelia Livingston
-/
import Mathlib.Algebra.Category.AlgebraCat.Basic
import Mathlib.Algebra.Category.Ring.Basic

/-!
# The category of commutative algebras

This file mimics `Mathlib.LinearAlgebra.QuadraticForm.QuadraticModuleCat`.

-/

open CategoryTheory

universe v u w v' w'

variable (R : Type u) [CommRing R]

/-- The category of commutative `R`-algebras. -/
structure CommAlgebraCat extends Bundled CommRing.{v} where
  [isAlgebra : Algebra R Î±]

attribute [instance] CommAlgebraCat.isAlgebra

variable {R}

namespace CommAlgebraCat

open Algebra

instance : CoeSort (CommAlgebraCat.{v} R) (Type v) :=
  âŸ¨(Â·.Î±)âŸ©

@[simp] theorem CommRingCat_of_toCommRingCat (X : CommAlgebraCat.{v} R) :
    CommRingCat.of X.Î± = X.Î± :=
  rfl

variable (R)

/-- The object in the category of commutative `R`-algebras associated to a
commutative `R`-algebra. -/
@[simps]
def of (X : Type v) [CommRing X] [Algebra R X] :
    CommAlgebraCat R where
  isAlgebra := (inferInstance : Algebra R X)

variable {R}

/-- A type alias for `AlgHom` to avoid confusion between the categorical and
algebraic spellings of composition. -/
@[ext]
structure Hom (V W : CommAlgebraCat.{v} R) :=
  /-- The underlying `AlgHom` -/
  toAlgHom : V â†’â‚[R] W

lemma Hom.toAlgHom_injective (V W : CommAlgebraCat.{v} R) :
    Function.Injective (Hom.toAlgHom : Hom V W â†’ _) :=
  fun âŸ¨fâŸ© âŸ¨gâŸ© _ => by congr

instance category : Category (CommAlgebraCat.{v} R) where
  Hom M N := Hom M N
  id M := âŸ¨AlgHom.id R MâŸ©
  comp f g := âŸ¨AlgHom.comp g.toAlgHom f.toAlgHomâŸ©

-- TODO: if `Quiver.Hom` and the instance above were `reducible`, this wouldn't be needed.
@[ext]
lemma hom_ext {M N : CommAlgebraCat.{v} R} (f g : M âŸ¶ N) (h : f.toAlgHom = g.toAlgHom) :
    f = g :=
  Hom.ext _ _ h

/-- Typecheck a `AlgHom` as a morphism in `CommAlgebraCat R`. -/
abbrev ofHom {X Y : Type v} [CommRing X] [Algebra R X] [CommRing Y] [Algebra R Y]
    (f : X â†’â‚[R] Y) : of R X âŸ¶ of R Y :=
  âŸ¨fâŸ©

@[simp] theorem toAlgHom_comp {M N U : CommAlgebraCat.{v} R} (f : M âŸ¶ N) (g : N âŸ¶ U) :
    (f â‰« g).toAlgHom = g.toAlgHom.comp f.toAlgHom :=
  rfl

@[simp] theorem toAlgHom_id {M : CommAlgebraCat.{v} R} :
    Hom.toAlgHom (ðŸ™ M) = AlgHom.id _ _ :=
  rfl

instance concreteCategory : ConcreteCategory.{v} (CommAlgebraCat.{v} R) where
  forget :=
    { obj := fun M => M
      map := fun f => f.toAlgHom }
  forget_faithful :=
    { map_injective := fun {M N} f g h => hom_ext _ _ <| DFunLike.coe_injective h }

instance hasForgetToAlgebra : HasForgetâ‚‚ (CommAlgebraCat R) (AlgebraCat R) where
  forgetâ‚‚ :=
    { obj := fun M => AlgebraCat.of R M
      map := fun f => f.toAlgHom }

@[simp]
theorem forgetâ‚‚_algebra_obj (X : CommAlgebraCat R) :
    (forgetâ‚‚ (CommAlgebraCat R) (AlgebraCat R)).obj X = AlgebraCat.of R X :=
  rfl

@[simp]
theorem forgetâ‚‚_algebra_map (X Y : CommAlgebraCat R) (f : X âŸ¶ Y) :
    (forgetâ‚‚ (CommAlgebraCat R) (AlgebraCat R)).map f = f.toAlgHom :=
  rfl

instance hasForgetToCommRing : HasForgetâ‚‚ (CommAlgebraCat R) CommRingCat where
  forgetâ‚‚ :=
    { obj := fun A => CommRingCat.of A
      map := fun {A B} f => (f.toAlgHom : A â†’+* B) }

@[simp]
theorem forgetâ‚‚_commRing_obj (X : CommAlgebraCat R) :
    (forgetâ‚‚ (CommAlgebraCat R) CommRingCat).obj X = CommRingCat.of X :=
  rfl

@[simp]
theorem forgetâ‚‚_commRing_map (X Y : CommAlgebraCat R) (f : X âŸ¶ Y) :
    (forgetâ‚‚ (CommAlgebraCat R) CommRingCat).map f = (f.toAlgHom : X â†’+* Y) :=
  rfl

end CommAlgebraCat

namespace AlgEquiv

open CommAlgebraCat

variable {X Y Z : Type v}
variable [CommRing X] [Algebra R X] [CommRing Y] [Algebra R Y] [CommRing Z] [Algebra R Z]

/-- Build an isomorphism in the category `CommAlgebraCat R` from a
`AlgEquiv`. -/
@[simps]
def toCommAlgebraIso (e : X â‰ƒâ‚[R] Y) : CommAlgebraCat.of R X â‰… CommAlgebraCat.of R Y where
  hom := CommAlgebraCat.ofHom e
  inv := CommAlgebraCat.ofHom e.symm

@[simp] theorem toCommAlgebraIso_refl :
    toCommAlgebraIso (AlgEquiv.refl (R := R) (Aâ‚ := X)) = .refl _ :=
  rfl

@[simp] theorem toCommAlgebraIso_symm (e : X â‰ƒâ‚[R] Y) :
    toCommAlgebraIso e.symm = (toCommAlgebraIso e).symm :=
  rfl

@[simp] theorem toCommAlgebraIso_trans (e : X â‰ƒâ‚[R] Y) (f : Y â‰ƒâ‚[R] Z) :
    toCommAlgebraIso (e.trans f) = toCommAlgebraIso e â‰ªâ‰« toCommAlgebraIso f :=
  rfl

end AlgEquiv

namespace CategoryTheory.Iso

open Algebra

variable {X Y Z : CommAlgebraCat.{v} R}

/-- Build a `AlgEquiv` from an isomorphism in the category
`CommAlgebraCat R`. -/
def toCommAlgEquiv (i : X â‰… Y) : X â‰ƒâ‚[R] Y :=
  { i.hom.toAlgHom with
    invFun := i.inv.toAlgHom
    left_inv := fun x => AlgHom.congr_fun (congr_arg CommAlgebraCat.Hom.toAlgHom i.3) x
    right_inv := fun x => AlgHom.congr_fun (congr_arg CommAlgebraCat.Hom.toAlgHom i.4) x }

@[simp] theorem toCommAlgEquiv_toAlgHom (i : X â‰… Y) :
    i.toCommAlgEquiv = i.hom.toAlgHom := rfl

@[simp] theorem toCommAlgEquiv_refl : toCommAlgEquiv (.refl X) = .refl :=
  rfl

@[simp] theorem toCommAlgEquiv_symm (e : X â‰… Y) :
    toCommAlgEquiv e.symm = (toCommAlgEquiv e).symm :=
  rfl

@[simp] theorem toCommAlgEquiv_trans (e : X â‰… Y) (f : Y â‰… Z) :
    toCommAlgEquiv (e â‰ªâ‰« f) = e.toCommAlgEquiv.trans f.toCommAlgEquiv :=
  rfl

end CategoryTheory.Iso
