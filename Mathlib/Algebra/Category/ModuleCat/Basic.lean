/-
Copyright (c) 2019 Robert A. Spencer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robert A. Spencer, Markus Himmel
-/
import Mathlib.Algebra.Category.Grp.Preadditive
import Mathlib.Algebra.Module.Equiv.Basic
import Mathlib.Algebra.Module.PUnit
import Mathlib.CategoryTheory.Conj
import Mathlib.CategoryTheory.Linear.Basic
import Mathlib.CategoryTheory.Preadditive.AdditiveFunctor

/-!
# The category of `R`-modules

`ModuleCat.{v} R` is the category of bundled `R`-modules with carrier in the universe `v`. We show
that it is preadditive and show that being an isomorphism, monomorphism and epimorphism is
equivalent to being a linear equivalence, an injective linear map and a surjective linear map,
respectively.

## Implementation details

To construct an object in the category of `R`-modules from a type `M` with an instance of the
`Module` typeclass, write `of R M`. There is a coercion in the other direction.
The roundtrip `‚Üë(of R M)` is definitionally equal to `M` itself (when `M` is a type with `Module`
instance), and so is `of R ‚ÜëM` (when `M : ModuleCat R M`).

The morphisms are given their own type, not identified with `LinearMap`.
There is a cast from morphisms in `Module R` to linear maps, written `f.hom` (`ModuleCat.Hom.hom`).
To go from linear maps to morphisms in `Module R`, use `ModuleCat.ofHom`.

Similarly, given an isomorphism `f : M ‚âÖ N` use `f.toLinearEquiv` and given a linear equiv
`f : M ‚âÉ‚Çó[R] N`, use `f.toModuleIso`.
-/


open CategoryTheory

open CategoryTheory.Limits

open CategoryTheory.Limits.WalkingParallelPair

universe v u

variable (R : Type u) [Ring R]

/-- The category of R-modules and their morphisms.

Note that in the case of `R = ‚Ñ§`, we can not
impose here that the `‚Ñ§`-multiplication field from the module structure is defeq to the one coming
from the `isAddCommGroup` structure (contrary to what we do for all module structures in
mathlib), which creates some difficulties down the road. -/
structure ModuleCat where
  private mk ::
  /-- the underlying type of an object in `ModuleCat R` -/
  carrier : Type v
  [isAddCommGroup : AddCommGroup carrier]
  [isModule : Module R carrier]

attribute [instance] ModuleCat.isAddCommGroup ModuleCat.isModule

namespace ModuleCat

instance : CoeSort (ModuleCat.{v} R) (Type v) :=
  ‚ü®ModuleCat.carrier‚ü©

attribute [coe] ModuleCat.carrier

/-- The object in the category of R-algebras associated to a type equipped with the appropriate
typeclasses. This is the preferred way to construct a term of `ModuleCat R`. -/
abbrev of (X : Type v) [AddCommGroup X] [Module R X] : ModuleCat.{v} R :=
  ‚ü®X‚ü©

lemma coe_of (X : Type v) [Ring X] [Module R X] : (of R X : Type v) = X :=
  rfl

-- Ensure the roundtrips are reducibly defeq (so tactics like `rw` can see through them).
example (X : Type v) [Ring X] [Module R X] : (of R X : Type v) = X := by with_reducible rfl
example (M : ModuleCat.{v} R) : of R M = M := by with_reducible rfl

variable {R} in
/-- The type of morphisms in `ModuleCat R`. -/
@[ext]
structure Hom (M N : ModuleCat.{v} R) where
  private mk ::
  /-- The underlying linear map. -/
  hom' : M ‚Üí‚Çó[R] N

instance moduleCategory : Category.{v, max (v+1) u} (ModuleCat.{v} R) where
  Hom M N := Hom M N
  id _ := ‚ü®LinearMap.id‚ü©
  comp f g := ‚ü®g.hom'.comp f.hom'‚ü©

instance : ConcreteCategory (ModuleCat.{v} R) (¬∑ ‚Üí‚Çó[R] ¬∑) where
  hom := Hom.hom'
  ofHom := Hom.mk

section

variable {R}

/-- Turn a morphism in `ModuleCat` back into a `LinearMap`. -/
abbrev Hom.hom {A B : ModuleCat.{v} R} (f : Hom A B) :=
  ConcreteCategory.hom (C := ModuleCat R) f

/-- Typecheck a `LinearMap` as a morphism in `ModuleCat`. -/
abbrev ofHom {X Y : Type v} [AddCommGroup X] [Module R X] [AddCommGroup Y] [Module R Y]
    (f : X ‚Üí‚Çó[R] Y) : of R X ‚ü∂ of R Y :=
  ConcreteCategory.ofHom (C := ModuleCat R) f

/-- Use the `ConcreteCategory.hom` projection for `@[simps]` lemmas. -/
def Hom.Simps.hom (A B : ModuleCat.{v} R) (f : Hom A B) :=
  f.hom

initialize_simps_projections Hom (hom' ‚Üí hom)

/-!
The results below duplicate the `ConcreteCategory` simp lemmas, but we can keep them for `dsimp`.
-/

@[simp]
lemma hom_id {M : ModuleCat.{v} R} : (ùüô M : M ‚ü∂ M).hom = LinearMap.id := rfl

/- Provided for rewriting. -/
lemma id_apply (M : ModuleCat.{v} R) (x : M) :
    (ùüô M : M ‚ü∂ M) x = x := by simp

@[simp]
lemma hom_comp {M N O : ModuleCat.{v} R} (f : M ‚ü∂ N) (g : N ‚ü∂ O) :
    (f ‚â´ g).hom = g.hom.comp f.hom := rfl

/- Provided for rewriting. -/
lemma comp_apply {M N O : ModuleCat.{v} R} (f : M ‚ü∂ N) (g : N ‚ü∂ O) (x : M) :
    (f ‚â´ g) x = g (f x) := by simp

@[ext]
lemma hom_ext {M N : ModuleCat.{v} R} {f g : M ‚ü∂ N} (hf : f.hom = g.hom) : f = g :=
  Hom.ext hf

lemma hom_bijective {M N : ModuleCat.{v} R} :
    Function.Bijective (Hom.hom : (M ‚ü∂ N) ‚Üí (M ‚Üí‚Çó[R] N)) where
  left f g h := by cases f; cases g; simpa using h
  right f := ‚ü®‚ü®f‚ü©, rfl‚ü©

/-- Convenience shortcut for `ModuleCat.hom_bijective.injective`. -/
lemma hom_injective {M N : ModuleCat.{v} R} :
    Function.Injective (Hom.hom : (M ‚ü∂ N) ‚Üí (M ‚Üí‚Çó[R] N)) :=
  hom_bijective.injective

/-- Convenience shortcut for `ModuleCat.hom_bijective.surjective`. -/
lemma hom_surjective {M N : ModuleCat.{v} R} :
    Function.Surjective (Hom.hom : (M ‚ü∂ N) ‚Üí (M ‚Üí‚Çó[R] N)) :=
  hom_bijective.surjective

@[simp]
lemma hom_ofHom {X Y : Type v} [AddCommGroup X] [Module R X] [AddCommGroup Y]
    [Module R Y] (f : X ‚Üí‚Çó[R] Y) : (ofHom f).hom = f := rfl

@[simp]
lemma ofHom_hom {M N : ModuleCat.{v} R} (f : M ‚ü∂ N) :
    ofHom (Hom.hom f) = f := rfl

@[simp]
lemma ofHom_id {M : Type v} [AddCommGroup M] [Module R M] : ofHom LinearMap.id = ùüô (of R M) := rfl

@[simp]
lemma ofHom_comp {M N O : Type v} [AddCommGroup M] [AddCommGroup N] [AddCommGroup O] [Module R M]
    [Module R N] [Module R O] (f : M ‚Üí‚Çó[R] N) (g : N ‚Üí‚Çó[R] O) :
    ofHom (g.comp f) = ofHom f ‚â´ ofHom g :=
  rfl

/- Doesn't need to be `@[simp]` since `simp only` can solve this. -/
lemma ofHom_apply {M N : Type v} [AddCommGroup M] [AddCommGroup N] [Module R M] [Module R N]
    (f : M ‚Üí‚Çó[R] N) (x : M) : ofHom f x = f x := rfl

lemma inv_hom_apply {M N : ModuleCat.{v} R} (e : M ‚âÖ N) (x : M) : e.inv (e.hom x) = x := by
  simp

lemma hom_inv_apply {M N : ModuleCat.{v} R} (e : M ‚âÖ N) (x : N) : e.hom (e.inv x) = x := by
  simp

/-- `ModuleCat.Hom.hom` bundled as an `Equiv`. -/
def homEquiv {M N : ModuleCat.{v} R} : (M ‚ü∂ N) ‚âÉ (M ‚Üí‚Çó[R] N) where
  toFun := Hom.hom
  invFun := ofHom

end

/- Not a `@[simp]` lemma since it will rewrite the (co)domain of maps and cause
definitional equality issues. -/
lemma forget_obj {M : ModuleCat.{v} R} : (forget (ModuleCat.{v} R)).obj M = M := rfl

@[simp]
lemma forget_map {M N : ModuleCat.{v} R} (f : M ‚ü∂ N) :
    (forget (ModuleCat.{v} R)).map f = f :=
  rfl

instance hasForgetToAddCommGroup : HasForget‚ÇÇ (ModuleCat R) AddCommGrp where
  forget‚ÇÇ :=
    { obj := fun M ‚Ü¶ AddCommGrp.of M
      map := fun f ‚Ü¶ AddCommGrp.ofHom f.hom.toAddMonoidHom }

@[simp]
theorem forget‚ÇÇ_obj (X : ModuleCat R) :
    (forget‚ÇÇ (ModuleCat R) AddCommGrp).obj X = AddCommGrp.of X :=
  rfl

theorem forget‚ÇÇ_obj_moduleCat_of (X : Type v) [AddCommGroup X] [Module R X] :
    (forget‚ÇÇ (ModuleCat R) AddCommGrp).obj (of R X) = AddCommGrp.of X :=
  rfl

@[simp]
theorem forget‚ÇÇ_map (X Y : ModuleCat R) (f : X ‚ü∂ Y) :
    (forget‚ÇÇ (ModuleCat R) AddCommGrp).map f = AddCommGrp.ofHom f.hom :=
  rfl

instance : Inhabited (ModuleCat R) :=
  ‚ü®of R PUnit‚ü©

@[simp] theorem of_coe (X : ModuleCat R) : of R X = X := rfl

variable {R}

/-- Forgetting to the underlying type and then building the bundled object returns the original
module. -/
@[deprecated Iso.refl (since := "2025-05-15")]
def ofSelfIso (M : ModuleCat R) : ModuleCat.of R M ‚âÖ M where
  hom := ùüô M
  inv := ùüô M

theorem isZero_of_subsingleton (M : ModuleCat R) [Subsingleton M] : IsZero M where
  unique_to X := ‚ü®‚ü®‚ü®ofHom (0 : M ‚Üí‚Çó[R] X)‚ü©, fun f ‚Ü¶ by
    ext x
    rw [Subsingleton.elim x (0 : M)]
    simp‚ü©‚ü©
  unique_from X := ‚ü®‚ü®‚ü®ofHom (0 : X ‚Üí‚Çó[R] M)‚ü©, fun f ‚Ü¶ by
    ext x
    subsingleton‚ü©‚ü©

instance : HasZeroObject (ModuleCat.{v} R) :=
  ‚ü®‚ü®of R PUnit, isZero_of_subsingleton _‚ü©‚ü©

end ModuleCat

variable {R}
variable {X‚ÇÅ X‚ÇÇ : Type v}

open ModuleCat

/-- Reinterpreting a linear map in the category of `R`-modules -/
scoped[ModuleCat] notation "‚Üü" f:1024 => ModuleCat.ofHom f

section

/-- Build an isomorphism in the category `Module R` from a `LinearEquiv` between `Module`s. -/
@[simps]
def LinearEquiv.toModuleIso {g‚ÇÅ : AddCommGroup X‚ÇÅ} {g‚ÇÇ : AddCommGroup X‚ÇÇ} {m‚ÇÅ : Module R X‚ÇÅ}
    {m‚ÇÇ : Module R X‚ÇÇ} (e : X‚ÇÅ ‚âÉ‚Çó[R] X‚ÇÇ) : ModuleCat.of R X‚ÇÅ ‚âÖ ModuleCat.of R X‚ÇÇ where
  hom := ofHom (e : X‚ÇÅ ‚Üí‚Çó[R] X‚ÇÇ)
  inv := ofHom (e.symm : X‚ÇÇ ‚Üí‚Çó[R] X‚ÇÅ)
  hom_inv_id := by ext; apply e.left_inv
  inv_hom_id := by ext; apply e.right_inv

namespace CategoryTheory.Iso

/-- Build a `LinearEquiv` from an isomorphism in the category `ModuleCat R`. -/
def toLinearEquiv {X Y : ModuleCat R} (i : X ‚âÖ Y) : X ‚âÉ‚Çó[R] Y :=
  LinearEquiv.ofLinear i.hom.hom i.inv.hom (by aesop) (by aesop)

end CategoryTheory.Iso

/-- linear equivalences between `Module`s are the same as (isomorphic to) isomorphisms
in `ModuleCat` -/
@[simps]
def linearEquivIsoModuleIso {X Y : Type u} [AddCommGroup X] [AddCommGroup Y] [Module R X]
    [Module R Y] : (X ‚âÉ‚Çó[R] Y) ‚âÖ ModuleCat.of R X ‚âÖ ModuleCat.of R Y where
  hom e := e.toModuleIso
  inv i := i.toLinearEquiv

end

namespace ModuleCat

section AddCommGroup

variable {M N : ModuleCat.{v} R}

instance : Add (M ‚ü∂ N) where
  add f g := ‚ü®f.hom + g.hom‚ü©

@[simp] lemma hom_add (f g : M ‚ü∂ N) : (f + g).hom = f.hom + g.hom := rfl

instance : Zero (M ‚ü∂ N) where
  zero := ‚ü®0‚ü©

@[simp] lemma hom_zero : (0 : M ‚ü∂ N).hom = 0 := rfl

instance : SMul ‚Ñï (M ‚ü∂ N) where
  smul n f := ‚ü®n ‚Ä¢ f.hom‚ü©

@[simp] lemma hom_nsmul (n : ‚Ñï) (f : M ‚ü∂ N) : (n ‚Ä¢ f).hom = n ‚Ä¢ f.hom := rfl

instance : Neg (M ‚ü∂ N) where
  neg f := ‚ü®-f.hom‚ü©

@[simp] lemma hom_neg (f : M ‚ü∂ N) : (-f).hom = -f.hom := rfl

instance : Sub (M ‚ü∂ N) where
  sub f g := ‚ü®f.hom - g.hom‚ü©

@[simp] lemma hom_sub (f g : M ‚ü∂ N) : (f - g).hom = f.hom - g.hom := rfl

instance : SMul ‚Ñ§ (M ‚ü∂ N) where
  smul n f := ‚ü®n ‚Ä¢ f.hom‚ü©

@[simp] lemma hom_zsmul (n : ‚Ñï) (f : M ‚ü∂ N) : (n ‚Ä¢ f).hom = n ‚Ä¢ f.hom := rfl

instance : AddCommGroup (M ‚ü∂ N) :=
  Function.Injective.addCommGroup (Hom.hom) hom_injective
    rfl (fun _ _ ‚Ü¶ rfl) (fun _ ‚Ü¶ rfl) (fun _ _ ‚Ü¶ rfl) (fun _ _ ‚Ü¶ rfl) (fun _ _ ‚Ü¶ rfl)

@[simp] lemma hom_sum {Œπ : Type*} (f : Œπ ‚Üí (M ‚ü∂ N)) (s : Finset Œπ) :
    (‚àë i ‚àà s, f i).hom = ‚àë i ‚àà s, (f i).hom :=
  map_sum ({ toFun := ModuleCat.Hom.hom, map_zero' := ModuleCat.hom_zero, map_add' := hom_add } :
    (M ‚ü∂ N) ‚Üí+ (M ‚Üí‚Çó[R] N)) _ _

instance : Preadditive (ModuleCat.{v} R) where

instance forget‚ÇÇ_addCommGrp_additive :
    (forget‚ÇÇ (ModuleCat.{v} R) AddCommGrp).Additive where

/-- `ModuleCat.Hom.hom` bundled as an additive equivalence. -/
@[simps!]
def homAddEquiv : (M ‚ü∂ N) ‚âÉ+ (M ‚Üí‚Çó[R] N) :=
  { homEquiv with
    map_add' := fun _ _ ‚Ü¶ rfl }

theorem subsingleton_of_isZero (h : IsZero M) : Subsingleton M := by
  refine subsingleton_of_forall_eq 0 (fun x ‚Ü¶ ?_)
  rw [‚Üê LinearMap.id_apply (R := R) x, ‚Üê ModuleCat.hom_id]
  simp only [(CategoryTheory.Limits.IsZero.iff_id_eq_zero M).mp h, hom_zero, LinearMap.zero_apply]

lemma isZero_iff_subsingleton : IsZero M ‚Üî Subsingleton M where
  mp := subsingleton_of_isZero
  mpr _ := isZero_of_subsingleton M

@[simp]
lemma isZero_of_iff_subsingleton {M : Type*} [AddCommGroup M] [Module R M] :
    IsZero (of R M) ‚Üî Subsingleton M := isZero_iff_subsingleton

end AddCommGroup

section SMul

variable {M N : ModuleCat.{v} R} {S : Type*} [Monoid S] [DistribMulAction S N] [SMulCommClass R S N]

instance : SMul S (M ‚ü∂ N) where
  smul c f := ‚ü®c ‚Ä¢ f.hom‚ü©

@[simp] lemma hom_smul (s : S) (f : M ‚ü∂ N) : (s ‚Ä¢ f).hom = s ‚Ä¢ f.hom := rfl

end SMul

section Module

variable {M N : ModuleCat.{v} R} {S : Type*} [Semiring S] [Module S N] [SMulCommClass R S N]

instance Hom.instModule : Module S (M ‚ü∂ N) :=
  Function.Injective.module S
    { toFun := Hom.hom, map_zero' := hom_zero, map_add' := hom_add }
    hom_injective
    (fun _ _ ‚Ü¶ rfl)

/-- `ModuleCat.Hom.hom` bundled as a linear equivalence. -/
@[simps]
def homLinearEquiv : (M ‚ü∂ N) ‚âÉ‚Çó[S] (M ‚Üí‚Çó[R] N) :=
  { homAddEquiv with
    map_smul' := fun _ _ ‚Ü¶ rfl }

end Module

section

universe u‚ÇÄ

namespace Algebra

variable {S‚ÇÄ : Type u‚ÇÄ} [CommSemiring S‚ÇÄ] {S : Type u} [Ring S] [Algebra S‚ÇÄ S]

variable {M N : ModuleCat.{v} S}

/--
Let `S` be an `S‚ÇÄ`-algebra. Then `S`-modules are modules over `S‚ÇÄ`.
-/
scoped instance : Module S‚ÇÄ M := Module.compHom _ (algebraMap S‚ÇÄ S)

scoped instance : IsScalarTower S‚ÇÄ S M where
  smul_assoc _ _ _ := by rw [Algebra.smul_def, mul_smul]; rfl

scoped instance : SMulCommClass S S‚ÇÄ M where
  smul_comm s s‚ÇÄ n :=
    show s ‚Ä¢ algebraMap S‚ÇÄ S s‚ÇÄ ‚Ä¢ n = algebraMap S‚ÇÄ S s‚ÇÄ ‚Ä¢ s ‚Ä¢ n by
    rw [‚Üê smul_assoc, smul_eq_mul, ‚Üê Algebra.commutes, mul_smul]

/--
Let `S` be an `S‚ÇÄ`-algebra. Then the category of `S`-modules is `S‚ÇÄ`-linear.
-/
scoped instance instLinear : Linear S‚ÇÄ (ModuleCat.{v} S) where
  smul_comp _ M N s‚ÇÄ f g := by ext; simp

end Algebra

section

variable {S : Type u} [CommRing S]

instance : Linear S (ModuleCat.{v} S) := ModuleCat.Algebra.instLinear

variable {X Y X' Y' : ModuleCat.{v} S}

theorem Iso.homCongr_eq_arrowCongr (i : X ‚âÖ X') (j : Y ‚âÖ Y') (f : X ‚ü∂ Y) :
    Iso.homCongr i j f = ‚ü®LinearEquiv.arrowCongr i.toLinearEquiv j.toLinearEquiv f.hom‚ü© :=
  rfl

theorem Iso.conj_eq_conj (i : X ‚âÖ X') (f : End X) :
    Iso.conj i f = ‚ü®LinearEquiv.conj i.toLinearEquiv f.hom‚ü© :=
  rfl

end

end

variable (M N : ModuleCat.{v} R)

/-- `ModuleCat.Hom.hom` as an isomorphism of rings. -/
@[simps!] def endRingEquiv : End M ‚âÉ+* (M ‚Üí‚Çó[R] M) where
  toFun := ModuleCat.Hom.hom
  invFun := ModuleCat.ofHom
  map_mul' _ _ := rfl
  map_add' _ _ := rfl

/-- The scalar multiplication on an object of `ModuleCat R` considered as
a morphism of rings from `R` to the endomorphisms of the underlying abelian group. -/
def smul : R ‚Üí+* End ((forget‚ÇÇ (ModuleCat R) AddCommGrp).obj M) where
  toFun r := AddCommGrp.ofHom
    { toFun := fun (m : M) ‚Ü¶ r ‚Ä¢ m
      map_zero' := by rw [smul_zero]
      map_add' := fun x y ‚Ü¶ by rw [smul_add] }
  map_one' := AddCommGrp.ext (fun x ‚Ü¶ by simp)
  map_zero' := AddCommGrp.ext (fun x ‚Ü¶ by simp)
  map_mul' r s := AddCommGrp.ext (fun (x : M) ‚Ü¶ (smul_smul r s x).symm)
  map_add' r s := AddCommGrp.ext (fun (x : M) ‚Ü¶ add_smul r s x)

lemma smul_naturality {M N : ModuleCat.{v} R} (f : M ‚ü∂ N) (r : R) :
    (forget‚ÇÇ (ModuleCat R) AddCommGrp).map f ‚â´ N.smul r =
      M.smul r ‚â´ (forget‚ÇÇ (ModuleCat R) AddCommGrp).map f := by
  ext x
  exact (f.hom.map_smul r x).symm

variable (R) in
/-- The scalar multiplication on `ModuleCat R` considered as a morphism of rings
to the endomorphisms of the forgetful functor to `AddCommGrp)`. -/
@[simps]
def smulNatTrans : R ‚Üí+* End (forget‚ÇÇ (ModuleCat R) AddCommGrp) where
  toFun r :=
    { app := fun M ‚Ü¶ M.smul r
      naturality := fun _ _ _ ‚Ü¶ smul_naturality _ r }
  map_one' := NatTrans.ext (by cat_disch)
  map_zero' := NatTrans.ext (by cat_disch)
  map_mul' _ _ := NatTrans.ext (by cat_disch)
  map_add' _ _ := NatTrans.ext (by cat_disch)

/-- Given `A : AddCommGrp` and a ring morphism `R ‚Üí+* End A`, this is a type synonym
for `A`, on which we shall define a structure of `R`-module. -/
@[nolint unusedArguments]
def mkOfSMul' {A : AddCommGrp} (_ : R ‚Üí+* End A) := A

section

variable {A : AddCommGrp} (œÜ : R ‚Üí+* End A)

instance : AddCommGroup (mkOfSMul' œÜ) := by
  dsimp only [mkOfSMul']
  infer_instance

instance : SMul R (mkOfSMul' œÜ) := ‚ü®fun r (x : A) ‚Ü¶ (show A ‚ü∂ A from œÜ r) x‚ü©

@[simp]
lemma mkOfSMul'_smul (r : R) (x : mkOfSMul' œÜ) :
    r ‚Ä¢ x = (show A ‚ü∂ A from œÜ r) x := rfl

instance : Module R (mkOfSMul' œÜ) where
  smul_zero _ := map_zero (N := A) _
  smul_add _ _ _ := map_add (N := A) _ _ _
  one_smul := by simp
  mul_smul := by simp
  add_smul _ _ _ := by simp; rfl
  zero_smul := by simp

/-- Given `A : AddCommGrp` and a ring morphism `R ‚Üí+* End A`, this is an object in
`ModuleCat R`, whose underlying abelian group is `A` and whose scalar multiplication is
given by `R`. -/
abbrev mkOfSMul := ModuleCat.of R (mkOfSMul' œÜ)

lemma mkOfSMul_smul (r : R) : (mkOfSMul œÜ).smul r = œÜ r := rfl

end

section

variable {M N}
  (œÜ : (forget‚ÇÇ (ModuleCat R) AddCommGrp).obj M ‚ü∂
      (forget‚ÇÇ (ModuleCat R) AddCommGrp).obj N)
  (hœÜ : ‚àÄ (r : R), œÜ ‚â´ N.smul r = M.smul r ‚â´ œÜ)

/-- Constructor for morphisms in `ModuleCat R` which takes as inputs
a morphism between the underlying objects in `AddCommGrp` and the compatibility
with the scalar multiplication. -/
@[simps]
def homMk : M ‚ü∂ N where
  hom'.toFun := œÜ
  hom'.map_add' _ _ := œÜ.hom.map_add _ _
  hom'.map_smul' r x := (congr_hom (hœÜ r) x).symm

lemma forget‚ÇÇ_map_homMk :
    (forget‚ÇÇ (ModuleCat R) AddCommGrp).map (homMk œÜ hœÜ) = œÜ := rfl

end

instance : (forget (ModuleCat.{v} R)).ReflectsIsomorphisms where
  reflects f _ :=
    (inferInstance : IsIso ((LinearEquiv.mk f.hom
      (asIso ((forget (ModuleCat R)).map f)).toEquiv.invFun
      (Equiv.left_inv _) (Equiv.right_inv _)).toModuleIso).hom)

instance : (forget‚ÇÇ (ModuleCat.{v} R) AddCommGrp.{v}).ReflectsIsomorphisms where
  reflects f _ := by
    have : IsIso ((forget _).map f) := by
      change IsIso ((forget _).map ((forget‚ÇÇ _ AddCommGrp).map f))
      infer_instance
    apply isIso_of_reflects_iso _ (forget _)

end ModuleCat

section Bilinear

variable {R : Type*} [CommRing R]

namespace ModuleCat

/-- Turn a bilinear map into a homomorphism. -/
@[simps!]
def ofHom‚ÇÇ {M N P : ModuleCat.{u} R} (f : M ‚Üí‚Çó[R] N ‚Üí‚Çó[R] P) :
    M ‚ü∂ of R (N ‚ü∂ P) :=
  ofHom <| homLinearEquiv.symm.toLinearMap ‚àò‚Çó f

/-- Turn a homomorphism into a bilinear map. -/
@[simps!]
def Hom.hom‚ÇÇ {M N P : ModuleCat.{u} R}
    -- We write `Hom` instead of `M ‚ü∂ (of R (N ‚ü∂ P))`, otherwise dot notation breaks
    -- since it is expecting the type of `f` to be `ModuleCat.Hom`, not `Quiver.Hom`.
    (f : Hom M (of R (N ‚ü∂ P))) :
    M ‚Üí‚Çó[R] N ‚Üí‚Çó[R] P :=
  Hom.hom (by convert (f ‚â´ ofHom homLinearEquiv.toLinearMap))

@[simp] lemma Hom.hom‚ÇÇ_ofHom‚ÇÇ {M N P : ModuleCat.{u} R} (f : M ‚Üí‚Çó[R] N ‚Üí‚Çó[R] P) :
    (ofHom‚ÇÇ f).hom‚ÇÇ = f := rfl

@[simp] lemma ofHom‚ÇÇ_hom‚ÇÇ {M N P : ModuleCat.{u} R} (f : M ‚ü∂ of R (N ‚ü∂ P)) :
    ofHom‚ÇÇ f.hom‚ÇÇ = f := rfl

end ModuleCat

end Bilinear

/-!
`@[simp]` lemmas for `LinearMap.comp` and categorical identities.
-/

@[simp] theorem LinearMap.comp_id_moduleCat
    {R} [Ring R] {G : ModuleCat.{u} R} {H : Type u} [AddCommGroup H] [Module R H] (f : G ‚Üí‚Çó[R] H) :
    f.comp (ùüô G : G ‚ü∂ G).hom = f := by simp

@[simp] theorem LinearMap.id_moduleCat_comp
    {R} [Ring R] {G : Type u} [AddCommGroup G] [Module R G] {H : ModuleCat.{u} R} (f : G ‚Üí‚Çó[R] H) :
    LinearMap.comp (ùüô H : H ‚ü∂ H).hom f = f := by simp
