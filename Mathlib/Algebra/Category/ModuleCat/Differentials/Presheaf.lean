/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Category.ModuleCat.Presheaf.Pullback
import Mathlib.Algebra.Category.ModuleCat.Differentials.Basic
import Mathlib.Algebra.Category.Ring.Constructions

/-!
# The presheaf of differentials

In this file, we define the type `M.Derivation Ï†` of derivations
with values in a presheaf of `R`-modules `M` relative to
a morphism of `Ï† : S âŸ¶ F.op â‹™ R` of presheaves of commutative rings
over categories `C` and `D` that are related by a functor `F : C â¥¤ D`.
We formalize the notion of universal derivation.

Geometrically, if `f : X âŸ¶ S` is a morphisms of schemes (or more generally
a morphism of commutative ringed spaces), we would like to apply
these definitions in the case where `F` is the pullback functors from
open subsets of `S` to open subsets of `X` and `Ï†` is the
morphism $O_S âŸ¶ f_* O_X$.

In order to prove that there exists a universal derivation, the target
of which shall be called the presheaf of relative differentials of `Ï†`,
we first study the case where `F` is the identity functor.
In this case where we have a morphism of presheaves of commutative
rings `Ï†' : S' âŸ¶ R`, we construct a derivation
`DifferentialsConstruction.derivation'` which is universal.
Then, the general case is obtained by observing that
derivations for `S âŸ¶ F.op â‹™ R` identify to derivations
for `S' âŸ¶ R` where `S'` is the pullback by `F` of the presheaf of
commutative rings `S` (the data is the same: it suffices
to show that the two vanishing conditions `d_app` are equivalent).

-/

universe v u vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

open CategoryTheory Limits

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D]
  {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E]

namespace PresheafOfModules

variable {S : Cáµ’áµ– â¥¤ CommRingCat.{u}} {F : C â¥¤ D} {G : D â¥¤ E}
  {S' R : Dáµ’áµ– â¥¤ CommRingCat.{u}} {T : Eáµ’áµ– â¥¤ CommRingCat.{u}}
  (M N : PresheafOfModules.{v} (R â‹™ forgetâ‚‚ _ _))
  (Ï† : S âŸ¶ F.op â‹™ R) (Ï†' : S' âŸ¶ R)

/-- Given a morphism of presheaves of commutative rings `Ï† : S âŸ¶ F.op â‹™ R`,
this is the type of relative `Ï†`-derivation of a presheaf of `R`-modules `M`. -/
@[ext]
structure Derivation where
  /-- the underlying additive map `R.obj X â†’+ M.obj X` of a derivation -/
  d {X : Dáµ’áµ–} : R.obj X â†’+ M.obj X
  d_mul {X : Dáµ’áµ–} (a b : R.obj X) : d (a * b) = a â€¢ d b + b â€¢ d a := by aesop_cat
  d_map {X Y : Dáµ’áµ–} (f : X âŸ¶ Y) (x : R.obj X) :
    d (R.map f x) = M.map f (d x) := by aesop_cat
  d_app {X : Cáµ’áµ–} (a : S.obj X) : d (Ï†.app X a) = 0 := by aesop_cat

namespace Derivation

-- Note: `d_app` cannot be a simp lemma because `dsimp` would
-- simplify the composition of functors `R â‹™ forgetâ‚‚ _ _`
attribute [simp] d_mul d_map

section AddCommGroup

instance : Zero (M.Derivation Ï†) where
  zero := { d := 0 }

@[simp] lemma zero_d_apply {X : Dáµ’áµ–} (x : R.obj X) :
    (0 : M.Derivation Ï†).d x = 0 := rfl

variable {M Ï†}

instance : Neg (M.Derivation Ï†) where
  neg d :=
    { d := -d.d
      d_mul := fun a b â†¦ by dsimp; simp only [d_mul, smul_neg]; abel
      d_app := by intros; dsimp; rw [neg_eq_zero]; apply d_app }

@[simp] lemma neg_d_apply (d : M.Derivation Ï†) {X : Dáµ’áµ–} (x : R.obj X) :
    (-d).d x = -d.d x := rfl

instance : Add (M.Derivation Ï†) where
  add dâ‚ dâ‚‚ :=
    { d := dâ‚.d + dâ‚‚.d
      d_mul := by intros; dsimp; simp only [d_mul, smul_add]; abel
      d_map := by simp
      d_app := fun _ â†¦ by
        dsimp
        erw [d_app, d_app, add_zero] }

@[simp] lemma add_d_apply (d d' : M.Derivation Ï†) {X : Dáµ’áµ–} (x : R.obj X) :
    (d + d').d x = d.d x + d'.d x := rfl

instance : Sub (M.Derivation Ï†) where
  sub dâ‚ dâ‚‚ :=
    { d := dâ‚.d - dâ‚‚.d
      d_mul := by intros; dsimp; simp only [d_mul, smul_sub]; abel
      d_map := by simp
      d_app := fun _ â†¦ by
        dsimp
        erw [d_app, d_app, sub_zero] }

@[simp] lemma sub_d_apply (d d' : M.Derivation Ï†) {X : Dáµ’áµ–} (x : R.obj X) :
    (d - d').d x = d.d x - d'.d x := rfl

instance : AddCommGroup (M.Derivation Ï†) where
  add_assoc _ _ _ := by ext; dsimp; rw [add_assoc]
  zero_add _ := by ext; dsimp; rw [zero_add]
  add_zero _ := by ext; dsimp; rw [add_zero]
  neg_add_cancel _ := by ext; dsimp; rw [neg_add_cancel]
  add_comm _ _ := by ext; dsimp; rw [add_comm]
  sub_eq_add_neg _ _ := by ext; dsimp; rw [sub_eq_add_neg]
  nsmul := nsmulRec
  zsmul := zsmulRec

end AddCommGroup

variable {M N Ï†}

lemma congr_d {d d' : M.Derivation Ï†} (h : d = d') {X : Dáµ’áµ–} (b : R.obj X) :
    d.d b = d'.d b := by rw [h]

variable (d : M.Derivation Ï†)

@[simp] lemma d_one (X : Dáµ’áµ–) : d.d (X := X) 1 = 0 := by
  simpa using d.d_mul (X := X) 1 1

lemma d_zsmul (n : â„¤) {X : Dáµ’áµ–} (x : R.obj X) : d.d (n â€¢ x) = n â€¢ d.d x := by
  rw [map_zsmul]

@[simp]
lemma d_int_eq_zero (X : Dáµ’áµ–) (n : â„¤) : d.d (X := X) n = 0 := by
  trans d.d (n â€¢ 1)
  Â· simp
  Â· rw [d_zsmul, d_one, zsmul_zero]

@[simp]
lemma d_ulift_int_eq_zero (X : Dáµ’áµ–) (f : CommRingCat.of (ULift.{u} â„¤) âŸ¶ R.obj X)
    (n : ULift.{u} â„¤) :
    d.d (X := X) (f n) = 0 := by
  obtain âŸ¨f, rflâŸ© := CommRingCat.isInitial.hom_ext f
    (CommRingCat.ofHom ((Int.castRingHom (R.obj X)).comp ULift.ringEquiv.toRingHom))
  apply d_int_eq_zero

/-- The postcomposition of a derivation by a morphism of presheaves of modules. -/
@[simps! d_apply]
def postcomp (f : M âŸ¶ N) : N.Derivation Ï† where
  d := (f.app _).hom.toAddMonoidHom.comp d.d
  d_map {X Y} g x := by simpa using naturality_apply f g (d.d x)
  d_app {X} a := by
    dsimp
    erw [d_app]
    rw [map_zero]

variable (N) in
@[simp]
lemma postcomp_zero : d.postcomp (0 : _ âŸ¶ N) = 0 := rfl

lemma postcomp_comp {P : PresheafOfModules.{v} (R â‹™ forgetâ‚‚ _ _ )} (f : M âŸ¶ N) (g : N âŸ¶ P) :
    d.postcomp (f â‰« g) = (d.postcomp f).postcomp g := rfl

/-- The universal property that a derivation `d : M.Derivation Ï†` must
satisfy so that the presheaf of modules `M` can be considered as the presheaf of
(relative) differentials of a presheaf of commutative rings `Ï† : S âŸ¶ F.op â‹™ R`. -/
structure Universal where
  /-- An absolyte derivation of `M'` descends as a morphism `M âŸ¶ M'`. -/
  desc {M' : PresheafOfModules (R â‹™ forgetâ‚‚ CommRingCat RingCat)}
    (d' : M'.Derivation Ï†) : M âŸ¶ M'
  fac {M' : PresheafOfModules (R â‹™ forgetâ‚‚ CommRingCat RingCat)}
    (d' : M'.Derivation Ï†) : d.postcomp (desc d') = d' := by aesop_cat
  postcomp_injective {M' : PresheafOfModules (R â‹™ forgetâ‚‚ CommRingCat RingCat)}
    {Ï† Ï†' : M âŸ¶ M'} (h : d.postcomp Ï† = d.postcomp Ï†') : Ï† = Ï†' := by aesop_cat

attribute [simp] Universal.fac

instance : Subsingleton d.Universal where
  allEq hâ‚ hâ‚‚ := by
    suffices âˆ€ {M' : PresheafOfModules (R â‹™ forgetâ‚‚ CommRingCat RingCat)}
      (d' : M'.Derivation Ï†), hâ‚.desc d' = hâ‚‚.desc d' by
        cases hâ‚
        cases hâ‚‚
        simp only [Universal.mk.injEq]
        ext : 2
        apply this
    intro M' d'
    apply hâ‚.postcomp_injective
    simp

namespace Universal

variable {d} (hd : d.Universal)

@[simp]
lemma desc_postcomp {M' : PresheafOfModules.{v} (R â‹™ forgetâ‚‚ CommRingCat RingCat)}
    (f : M âŸ¶ M') : hd.desc (d.postcomp f) = f :=
  hd.postcomp_injective (by simp)

@[simps]
def homEquiv {M' : PresheafOfModules.{v} (R â‹™ forgetâ‚‚ CommRingCat RingCat)} :
    (M âŸ¶ M') â‰ƒ M'.Derivation Ï† where
  toFun f := d.postcomp f
  invFun d' := hd.desc d'
  left_inv f := by simp
  right_inv d' := by simp

lemma homEquiv_comp {M' M'' : PresheafOfModules.{v} (R â‹™ forgetâ‚‚ CommRingCat RingCat)}
    (f : M âŸ¶ M') (g : M' âŸ¶ M'') :
    hd.homEquiv (f â‰« g) = (hd.homEquiv f).postcomp g := rfl

end Universal

end Derivation

/-- The property that there exists a universal derivation for
a morphism of presheaves of commutative rings `S âŸ¶ F.op â‹™ R`. -/
class HasDifferentials : Prop where
  exists_universal_derivation : âˆƒ (M : PresheafOfModules.{u} (R â‹™ forgetâ‚‚ _ _))
      (d : M.Derivation Ï†), Nonempty d.Universal

lemma Derivation.Universal.hasDifferentials {M : PresheafOfModules.{u} (R â‹™ forgetâ‚‚ _ _)}
    {d : M.Derivation Ï†} (hd : d.Universal) : HasDifferentials Ï† :=
  âŸ¨_ ,_, âŸ¨hdâŸ©âŸ©

section

variable [HasDifferentials Ï†]

noncomputable def relativeDifferentials : PresheafOfModules.{u} (R â‹™ forgetâ‚‚ _ _) :=
  (HasDifferentials.exists_universal_derivation (Ï† := Ï†)).choose

noncomputable def universalDerivation : (relativeDifferentials Ï†).Derivation Ï† :=
  (HasDifferentials.exists_universal_derivation (Ï† := Ï†)).choose_spec.choose

noncomputable def universalUniversalDerivation : (universalDerivation Ï†).Universal :=
  (HasDifferentials.exists_universal_derivation (Ï† := Ï†)).choose_spec.choose_spec.some

end

/-- Given a morphism of presheaf of commutative rings `Ï† : S âŸ¶ F.op â‹™ R`,
this is functor which sends a presheaf of modules `M` to the abelian group `M.Derivation Ï†`. -/
@[simps]
def derivationFunctor :
    PresheafOfModules.{v} (R â‹™ forgetâ‚‚ CommRingCat RingCat) â¥¤ Ab where
  obj M := AddCommGrp.of (M.Derivation Ï†)
  map f := AddCommGrp.ofHom (AddMonoidHom.mk' (fun d â†¦ d.postcomp f) (by aesop_cat))

--@[simp]
--lemma derivationFunctor_map_apply
--    {M N : PresheafOfModules.{v} (R â‹™ forgetâ‚‚ CommRingCat RingCat)} (f : M âŸ¶ N)
--    (d : M.Derivation Ï†) :
--    0 = 1 := sorry
    --DFunLike.coe (Î± := M.Derivation Ï†) (Î² := fun _ â†¦ N.Derivation Ï†)
    --  ((derivationFunctor Ï†).map f) d = d.postcomp f := rfl

namespace Derivation

variable {M Ï†}

namespace Universal

@[simps]
def corepresentableBy {d : M.Derivation Ï†} (hd : d.Universal) :
    (derivationFunctor.{v} Ï† â‹™ forget _).CorepresentableBy M where
  homEquiv := hd.homEquiv
  homEquiv_comp _ _ := hd.homEquiv_comp _ _

end Universal

variable (h : (derivationFunctor.{v} Ï† â‹™ forget _).CorepresentableBy M)

def ofCorepresentableBy : M.Derivation Ï† := h.homEquiv (ğŸ™ _)

lemma ofCorepresentableBy_postcomp {M' : PresheafOfModules.{v} (R â‹™ forgetâ‚‚ _ _)} (f : M âŸ¶ M') :
    (ofCorepresentableBy h).postcomp f = h.homEquiv f := by
  simpa using (h.homEquiv_comp f (ğŸ™ _)).symm

def universalOfCorepresentableBy : (ofCorepresentableBy h).Universal where
  desc d := h.homEquiv.symm d
  fac {M'} d := by
    rw [ofCorepresentableBy_postcomp]
    apply Equiv.apply_symm_apply
  postcomp_injective H :=
    h.homEquiv.injective (by simpa only [ofCorepresentableBy_postcomp] using H)

end Derivation

/-- Given a morphism of presheaves of commutative rings `Ï† : S âŸ¶ R`,
this is the type of relative `Ï†`-derivation of a presheaf of `R`-modules `M`. -/
abbrev Derivation' : Type _ := M.Derivation (F := ğŸ­ D) Ï†'

namespace Derivation'

variable {M Ï†'}

@[simp]
nonrec lemma d_app (d : M.Derivation' Ï†') {X : Dáµ’áµ–} (a : S'.obj X) :
    d.d (Ï†'.app X a) = 0 :=
  d.d_app _

/-- The derivation relative to the morphism of commutative rings `Ï†'.app X` induced by
a derivation relative to a morphism of presheaves of commutative rings. -/
noncomputable def app (d : M.Derivation' Ï†') (X : Dáµ’áµ–) : (M.obj X).Derivation (Ï†'.app X) :=
  ModuleCat.Derivation.mk (fun b â†¦ d.d b)

@[simp]
lemma app_apply (d : M.Derivation' Ï†') {X : Dáµ’áµ–} (b : R.obj X) :
    (d.app X).d b = d.d b := rfl

section

variable (d : âˆ€ (X : Dáµ’áµ–), (M.obj X).Derivation (Ï†'.app X))

/-- Given a morphism of presheaves of commutative rings `Ï†'`, this is the
in derivation `M.Derivation' Ï†'` that is given by a compatible family of derivations
with values in the modules `M.obj X` for all `X`. -/
def mk (d_map : âˆ€ â¦ƒX Y : Dáµ’áµ–â¦„ (f : X âŸ¶ Y) (x : R.obj X),
    (d Y).d ((R.map f) x) = (M.map f) ((d X).d x)) : M.Derivation' Ï†' where
  d {X} := AddMonoidHom.mk' (d X).d (by simp)

variable (d_map : âˆ€ â¦ƒX Y : Dáµ’áµ–â¦„ (f : X âŸ¶ Y) (x : R.obj X),
      (d Y).d ((R.map f) x) = (M.map f) ((d X).d x))

@[simp]
lemma mk_app (X : Dáµ’áµ–) : (mk d d_map).app X = d X := rfl

/-- Constructor for `Derivation.Universal` in the case `F` is the identity functor. -/
def Universal.mk {d : M.Derivation' Ï†'}
    (desc : âˆ€ {M' : PresheafOfModules (R â‹™ forgetâ‚‚ _ _)}
      (_ : M'.Derivation' Ï†'), M âŸ¶ M')
    (fac : âˆ€ {M' : PresheafOfModules (R â‹™ forgetâ‚‚ _ _)}
      (d' : M'.Derivation' Ï†'), d.postcomp (desc d') = d')
    (postcomp_injective : âˆ€ {M' : PresheafOfModules (R â‹™ forgetâ‚‚ _ _)}
      {Î± Î² : M âŸ¶ M'}, d.postcomp Î± = d.postcomp Î² â†’ Î± = Î²) : d.Universal where
  desc := desc
  fac := fac
  postcomp_injective := postcomp_injective

end

end Derivation'

namespace DifferentialsConstruction

/-- The presheaf of relative differentials of a morphism of presheaves of
commutative rings. -/
@[simps -isSimp]
noncomputable def relativeDifferentials' :
    PresheafOfModules.{u} (R â‹™ forgetâ‚‚ _ _) where
  obj X := CommRingCat.KaehlerDifferential (Ï†'.app X)
  -- Have to hint `g' := R.map f` below, or it gets unfolded weirdly.
  map f := CommRingCat.KaehlerDifferential.map (g' := R.map f) (Ï†'.naturality f)
  -- Without `dsimp`, `ext` doesn't pick up the right lemmas.
  map_id _ := by dsimp; ext; simp
  map_comp _ _ := by dsimp; ext; simp

attribute [simp] relativeDifferentials'_obj

@[simp]
lemma relativeDifferentials'_map_d {X Y : Dáµ’áµ–} (f : X âŸ¶ Y) (x : R.obj X) :
    DFunLike.coe (Î± := CommRingCat.KaehlerDifferential (Ï†'.app X))
      (Î² := fun _ â†¦ CommRingCat.KaehlerDifferential (Ï†'.app Y))
      (ModuleCat.Hom.hom (R := â†‘(R.obj X)) ((relativeDifferentials' Ï†').map f))
        (CommRingCat.KaehlerDifferential.d x) =
        CommRingCat.KaehlerDifferential.d (R.map f x) :=
  CommRingCat.KaehlerDifferential.map_d (Ï†'.naturality f) _

/-- The universal derivation. -/
noncomputable def derivation' : (relativeDifferentials' Ï†').Derivation' Ï†' :=
  Derivation'.mk (fun X â†¦ CommRingCat.KaehlerDifferential.D (Ï†'.app X))
    (fun _ _ f x â†¦ (relativeDifferentials'_map_d Ï†' f x).symm)

/-- The derivation `Derivation' Ï†'` is universal. -/
noncomputable def isUniversal' : (derivation' Ï†').Universal :=
  Derivation'.Universal.mk
    (fun {M'} d' â†¦
      { app := fun X â†¦ (d'.app X).desc
        naturality := fun {X Y} f â†¦ CommRingCat.KaehlerDifferential.ext (fun b â†¦ by
          dsimp
          rw [ModuleCat.Derivation.desc_d, Derivation'.app_apply]
          erw [relativeDifferentials'_map_d Ï†' f]
          rw [ModuleCat.Derivation.desc_d]
          dsimp
          rw [Derivation.d_map]
          dsimp) })
    (fun {M'} d' â†¦ by
      ext X b
      apply ModuleCat.Derivation.desc_d)
    (fun {M} Î± Î² h â†¦ by
      ext1 X
      exact CommRingCat.KaehlerDifferential.ext (Derivation.congr_d h))

instance : HasDifferentials (F := ğŸ­ D) Ï†' := âŸ¨_, _, âŸ¨isUniversal' Ï†'âŸ©âŸ©

end DifferentialsConstruction

section

variable {Ï† M} {dÏ† : M.Derivation Ï†} (hdÏ† : dÏ†.Universal)
  {Ïˆ : R âŸ¶ G.op â‹™ T} {Ï†Ïˆ : S âŸ¶ (F â‹™ G).op â‹™ T} (fac : Ï†Ïˆ = Ï† â‰« whiskerLeft F.op Ïˆ)
  {P : PresheafOfModules.{v} (T â‹™ forgetâ‚‚ _ _)}

namespace Derivation

@[simps]
def induced {M' : PresheafOfModules.{v} (T â‹™ forgetâ‚‚ _ _)}
    (d : M'.Derivation Ïˆ) : M'.Derivation Ï†Ïˆ where
  d := d.d
  d_mul := by simp
  d_map := by simp
  d_app _ := by subst fac; apply d.d_app

local notation "pushforwardÏˆ" =>
  pushforward (F := G) (R := T â‹™ forgetâ‚‚ _ _) (Ï† := whiskerRight Ïˆ (forgetâ‚‚ _ RingCat))

local notation "pullbackÏˆ" =>
  pullback (F := G) (R := T â‹™ forgetâ‚‚ _ _) (Ï† := whiskerRight Ïˆ (forgetâ‚‚ _ RingCat))

local notation "adjunctionÏˆ" =>
  (pullbackPushforwardAdjunction
    (F := G) (R := T â‹™ forgetâ‚‚ _ _) (Ï† := whiskerRight Ïˆ (forgetâ‚‚ _ RingCat)))

variable (dÏ†Ïˆ : P.Derivation Ï†Ïˆ)

protected noncomputable def pushforward : ((pushforwardÏˆ).obj P).Derivation Ï† where
  d := AddMonoidHom.mk' (fun a â†¦ dÏ†Ïˆ.d (Ïˆ.app _ a)) (by simp)
  d_mul {X} a b := by
    dsimp
    rw [map_mul, dÏ†Ïˆ.d_mul]
  d_map {X Y} f a :=
    (congr_arg dÏ†Ïˆ.d (congr_fun ((forget _).congr_map (Ïˆ.naturality f)) a)).trans
      (dÏ†Ïˆ.d_map _ _)
  d_app a := by subst fac; exact dÏ†Ïˆ.d_app a

lemma pushforward_d_apply (Y : Dáµ’áµ–) (a : R.obj Y) :
    (Derivation.pushforward fac dÏ†Ïˆ).d a = dÏ†Ïˆ.d (Ïˆ.app _ a) := rfl

lemma pushforward_postcomp {P' : PresheafOfModules.{v} (T â‹™ forgetâ‚‚ _ _)} (Î± : P âŸ¶ P') :
    Derivation.pushforward fac (dÏ†Ïˆ.postcomp Î±) =
      (Derivation.pushforward fac dÏ†Ïˆ).postcomp ((pushforwardÏˆ).map Î±) := rfl

@[simp]
lemma pushforward_induced {M' : PresheafOfModules.{v} (T â‹™ forgetâ‚‚ _ _)} (d : M'.Derivation Ïˆ) :
    Derivation.pushforward fac (induced fac d) = 0 := by
  ext X a
  apply d.d_app

namespace Universal

noncomputable def pushforwardMap : M âŸ¶ (pushforwardÏˆ).obj P :=
  hdÏ†.desc (Derivation.pushforward fac dÏ†Ïˆ)

variable [(pushforward (F := G) (R := T â‹™ forgetâ‚‚ _ _)
  (whiskerRight Ïˆ (forgetâ‚‚ _ RingCat))).IsRightAdjoint]

noncomputable def pullbackMap : (pullbackÏˆ).obj M âŸ¶ P :=
  ((adjunctionÏˆ).homEquiv M P).symm (hdÏ†.pushforwardMap fac dÏ†Ïˆ)

lemma homEquiv_pullbackMap_comp
    {P' : PresheafOfModules.{v} (T â‹™ forgetâ‚‚ _ _)} (Î± : P âŸ¶ P') :
    (((adjunctionÏˆ).homEquiv _ _) (hdÏ†.pullbackMap fac dÏ†Ïˆ â‰« Î±)) =
      hdÏ†.homEquiv.symm (Derivation.pushforward fac (dÏ†Ïˆ.postcomp Î±)) := by
  apply hdÏ†.homEquiv.injective
  dsimp only [pullbackMap, pushforwardMap, pushforward_postcomp]
  simp only [homEquiv_apply, homEquiv_symm_apply, PresheafOfModules.Derivation.Universal.fac,
    Adjunction.homEquiv_naturality_right, Equiv.apply_symm_apply, postcomp_comp]

@[simp]
lemma pullbackMap_comp_eq_zero_iff
    {P' : PresheafOfModules.{v} (T â‹™ forgetâ‚‚ _ _)} (Î± : P âŸ¶ P') :
    hdÏ†.pullbackMap fac dÏ†Ïˆ â‰« Î± = 0 â†”
      Derivation.pushforward fac (dÏ†Ïˆ.postcomp Î±) = 0 := by
  rw [â† EmbeddingLike.apply_eq_iff_eq ((adjunctionÏˆ).homEquiv M P'),
    â† EmbeddingLike.apply_eq_iff_eq hdÏ†.homEquiv, homEquiv_pullbackMap_comp]
  simp only [homEquiv_symm_apply, homEquiv_apply, PresheafOfModules.Derivation.Universal.fac]
  rfl

variable {hdÏ† fac dÏ†Ïˆ}
  {c : CokernelCofork (hdÏ†.pullbackMap fac dÏ†Ïˆ)} (hc : IsColimit c) (hdÏ†Ïˆ : dÏ†Ïˆ.Universal)

namespace corepresentableByOfIsColimitCokernelCofork

variable {M' : PresheafOfModules.{v} (T â‹™ forgetâ‚‚ _ _)}

@[simps]
noncomputable def homEquivToFun (f : c.pt âŸ¶ M') : M'.Derivation Ïˆ where
  d := (dÏ†Ïˆ.postcomp (c.Ï€ â‰« f)).d
  d_map := by simp
  d_mul := by simp
  d_app := congr_d ((pullbackMap_comp_eq_zero_iff hdÏ† fac dÏ†Ïˆ (c.Ï€ â‰« f)).1 (by simp))

noncomputable def homEquivInvFun (d : M'.Derivation Ïˆ) : c.pt âŸ¶ M' :=
  (CokernelCofork.IsColimit.desc' hc (hdÏ†Ïˆ.desc (Derivation.induced fac d)) (by simp)).1

@[simp]
lemma Ï€_homEquivInvFun (d : M'.Derivation Ïˆ) :
    c.Ï€ â‰« homEquivInvFun hc hdÏ†Ïˆ d = hdÏ†Ïˆ.desc (Derivation.induced fac d) :=
  (CokernelCofork.IsColimit.desc' _ _ _).2

@[simp]
lemma homEquiv_left_inv (f : c.pt âŸ¶ M') :
    homEquivInvFun hc hdÏ†Ïˆ (homEquivToFun f) = f := by
  apply Cofork.IsColimit.hom_ext hc
  rw [Ï€_homEquivInvFun]
  apply hdÏ†Ïˆ.postcomp_injective
  rw [PresheafOfModules.Derivation.Universal.fac]
  ext
  dsimp

@[simp]
lemma homEquiv_right_inv (d : M'.Derivation Ïˆ) :
    homEquivToFun (homEquivInvFun hc hdÏ†Ïˆ d) = d := by
  ext : 2
  simp

end corepresentableByOfIsColimitCokernelCofork

open corepresentableByOfIsColimitCokernelCofork in
noncomputable def corepresentableByOfIsColimitCokernelCofork :
    (derivationFunctor Ïˆ â‹™ forget _).CorepresentableBy c.pt where
  homEquiv {M'} :=
    { toFun := homEquivToFun
      invFun := homEquivInvFun hc hdÏ†Ïˆ
      left_inv := fun _ â†¦ by simp
      right_inv := fun _ â†¦ by simp }
  homEquiv_comp _ _ := rfl

noncomputable def ofIsColimitCokernelCofork :
    (ofCorepresentableBy (corepresentableByOfIsColimitCokernelCofork hc hdÏ†Ïˆ)).Universal :=
  universalOfCorepresentableBy (corepresentableByOfIsColimitCokernelCofork hc hdÏ†Ïˆ)

end Universal

end Derivation

lemma hasDifferentials_of_tower
    [HasDifferentials Ï†] [HasDifferentials Ï†Ïˆ]
    [(pushforward.{u} (F := G) (R := T â‹™ forgetâ‚‚ _ _)
      (whiskerRight Ïˆ (forgetâ‚‚ _ RingCat))).IsRightAdjoint]
    (fac : Ï†Ïˆ = Ï† â‰« whiskerLeft F.op Ïˆ) :
    HasDifferentials Ïˆ :=
  âŸ¨cokernel (Derivation.Universal.pullbackMap
    (universalUniversalDerivation Ï†) fac _), _,
      âŸ¨Derivation.Universal.ofIsColimitCokernelCofork (colimit.isColimit _)
        (universalUniversalDerivation Ï†Ïˆ)âŸ©âŸ©

end

def absoluteDerivationEquiv
    (Ï† : (Functor.const Cáµ’áµ–).obj (CommRingCat.of (ULift.{u} â„¤)) âŸ¶ F.op â‹™ R)
    {M : PresheafOfModules.{u} (R â‹™ forgetâ‚‚ _ _)} :
    M.Derivation Ï† â‰ƒ M.Derivation (F := ğŸ­ D)
      (S := (Functor.const Dáµ’áµ–).obj (CommRingCat.of (ULift.{u} â„¤))) (R := R)
      { app := fun X â†¦ CommRingCat.isInitial.{u}.to _ } where
  toFun d :=
    { d := d.d
      d_mul := by simp
      d_map := by simp
      d_app _ := d.d_ulift_int_eq_zero _ _ _
        }
  invFun d :=
    { d := d.d
      d_mul := by simp
      d_map := by simp
      d_app _ := d.d_ulift_int_eq_zero _ _ _ }
  left_inv _ := rfl
  right_inv _ := rfl

def absoluteDerivationUniversalEquiv
    (Ï† : (Functor.const Cáµ’áµ–).obj (CommRingCat.of (ULift.{u} â„¤)) âŸ¶ F.op â‹™ R)
    (M : PresheafOfModules.{u} (R â‹™ forgetâ‚‚ _ _))
    (d : M.Derivation (F := ğŸ­ D)
      (S := (Functor.const Dáµ’áµ–).obj (CommRingCat.of (ULift.{u} â„¤))) (R := R)
      { app := fun X â†¦ CommRingCat.isInitial.{u}.to _ }) :
    d.Universal â‰ƒ ((absoluteDerivationEquiv Ï†).symm d).Universal where
  toFun hd :=
    { desc := fun d' â†¦ hd.desc (absoluteDerivationEquiv Ï† d')
      fac := fun d' â†¦ (absoluteDerivationEquiv Ï†).injective
        (hd.fac (absoluteDerivationEquiv Ï† d'))
      postcomp_injective :=
        fun h â†¦ hd.postcomp_injective ((absoluteDerivationEquiv Ï†).symm.injective h) }
  invFun hd :=
    { desc := fun d' â†¦ hd.desc ((absoluteDerivationEquiv Ï†).symm d')
      fac := fun d' â†¦ (absoluteDerivationEquiv Ï†).symm.injective
        (hd.fac ((absoluteDerivationEquiv Ï†).symm d'))
      postcomp_injective :=
        fun h â†¦ hd.postcomp_injective ((absoluteDerivationEquiv Ï†).injective h) }
  left_inv := fun _ â†¦ Subsingleton.elim _ _
  right_inv := fun _ â†¦ Subsingleton.elim _ _

instance hasAbsoluteDifferentials
    (Ï† : (Functor.const Cáµ’áµ–).obj (CommRingCat.of (ULift.{u} â„¤)) âŸ¶ F.op â‹™ R) :
    HasDifferentials Ï† :=
  ((absoluteDerivationUniversalEquiv Ï† _ _) (universalUniversalDerivation _)).hasDifferentials

instance hasDifferentials
    [(pushforward.{u} (F := F) (R := R â‹™ forgetâ‚‚ _ _)
      (whiskerRight Ï† (forgetâ‚‚ _ RingCat))).IsRightAdjoint] : HasDifferentials Ï† := by
  let Ï†â‚€ : (Functor.const _).obj (CommRingCat.of (ULift.{u} â„¤)) âŸ¶ S :=
    { app := fun X â†¦ CommRingCat.isInitial.{u}.to _ }
  exact hasDifferentials_of_tower (F := ğŸ­ C) (Ï† := Ï†â‚€) (Ïˆ := Ï†) (fac := rfl)

-- TODO: deduce the exact (cokernel) sequence of a tower from
-- Derivation.Universal.ofIsColimitCokernelCofork

end PresheafOfModules
