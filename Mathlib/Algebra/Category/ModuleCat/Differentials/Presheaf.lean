/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Category.ModuleCat.Presheaf
import Mathlib.Algebra.Category.ModuleCat.Differentials.Basic

/-!
# The presheaf of differentials of a presheaf of modules

In this file, we define the type `M.Derivation Ï†` of derivations
with values in a presheaf of `R`-modules `M` relative to
a morphism of `Ï† : S âŸ¶ F.op â‹™ R` of presheaves of commutative rings
over categories `C` and `D` that are related by a functor `F : C â¥¤ D`.
We formalize the notion of universal derivation.

Geometrically, if `f : X âŸ¶ S` is a morphisms of schemes (or more generally
a morphism of commutative ringed spaces), we would like to apply
these definitions in the case where `F` is the pullback functors from
open subsets of `S` to open subsets of `X` and `Ï†` is the
morphism $O_S âŸ¶ f_* O_X$.

In order to prove that there exists a universal derivation, the target
of which shall be called the presheaf of relative differentials of `Ï†`,
we first study the case where `F` is the identity functor.
In this case where we have a morphism of presheaves of commutative
rings `Ï†' : S' âŸ¶ R`, we construct a derivation
`DifferentialsConstruction.derivation'` which is universal (TODO).
Then, the general case (TODO) shall be obtained by observing that
derivations for `S âŸ¶ F.op â‹™ R` identify to derivations
for `S' âŸ¶ R` where `S'` is the pullback by `F` of the presheaf of
commutative rings `S` (the data is the same: it suffices
to show that the two vanishing conditions `d_app` are equivalent).

-/

universe v u vâ‚ vâ‚‚ uâ‚ uâ‚‚

open CategoryTheory

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D]

namespace PresheafOfModules

variable {S : Cáµ’áµ– â¥¤ CommRingCat.{u}} {F : C â¥¤ D} {S' R : Dáµ’áµ– â¥¤ CommRingCat.{u}}
   (M N : PresheafOfModules.{v} (R â‹™ forgetâ‚‚ _ _))
   (Ï† : S âŸ¶ F.op â‹™ R) (Ï†' : S' âŸ¶ R)

/-- Given a morphism of presheaves of commutative rings `Ï† : S âŸ¶ F.op â‹™ R`,
this is the type of relative `Ï†`-derivation of a presheaf of `R`-modules `M`. -/
structure Derivation where
  /-- the underlying additive map `R.obj X â†’+ M.obj X` of a derivation -/
  d {X : Dáµ’áµ–} : R.obj X â†’+ M.obj X
  d_mul {X : Dáµ’áµ–} (a b : R.obj X) : d (a * b) = a â€¢ d b + b â€¢ d a := by aesop_cat
  d_map {X Y : Dáµ’áµ–} (f : X âŸ¶ Y) (x : R.obj X) :
    d (R.map f x) = M.map f (d x) := by aesop_cat
  d_app {X : Cáµ’áµ–} (a : S.obj X) : d (Ï†.app X a) = 0 := by aesop_cat

namespace Derivation

-- Note: `d_app` cannot be a simp lemma because `dsimp` would
-- simplify the composition of functors `R â‹™ forgetâ‚‚ _ _`
attribute [simp] d_mul d_map

variable {M N Ï†}
variable (d : M.Derivation Ï†)

@[simp] lemma d_one (X : Dáµ’áµ–) : d.d (X := X) 1 = 0 := by
  simpa using d.d_mul (X := X) 1 1

/-- The postcomposition of a derivation by a morphism of presheaves of modules. -/
@[simps! d_apply]
def postcomp (f : M âŸ¶ N) : N.Derivation Ï† where
  d := (f.app _).toAddMonoidHom.comp d.d
  d_map _ _ := by simp [naturality_apply]
  d_app {X} a := by
    dsimp
    erw [d_app, map_zero]

/-- The universal property that a derivation `d : M.Derivation Ï†` must
satisfy so that the presheaf of modules `M` can be considered as the presheaf of
(relative) differentials of a presheaf of commutative rings `Ï† : S âŸ¶ F.op â‹™ R`. -/
structure Universal where
  /-- An absolyte derivation of `M'` descends as a morphism `M âŸ¶ M'`. -/
  desc {M' : PresheafOfModules (R â‹™ forgetâ‚‚ CommRingCat RingCat)}
    (d' : M'.Derivation Ï†) : M âŸ¶ M'
  fac {M' : PresheafOfModules (R â‹™ forgetâ‚‚ CommRingCat RingCat)}
    (d' : M'.Derivation Ï†) : d.postcomp (desc d') = d' := by aesop_cat
  postcomp_injective {M' : PresheafOfModules (R â‹™ forgetâ‚‚ CommRingCat RingCat)}
    (Ï† Ï†' : M âŸ¶ M') (h : d.postcomp Ï† = d.postcomp Ï†') : Ï† = Ï†' := by aesop_cat

end Derivation

/-- The property that there exists a universal derivation for
a morphism of presheaves of commutative rings `S âŸ¶ F.op â‹™ R`. -/
class HasDifferentials : Prop where
  exists_universal_derivation : âˆƒ (M : PresheafOfModules.{u} (R â‹™ forgetâ‚‚ _ _))
      (d : M.Derivation Ï†), Nonempty d.Universal

/-- Given a morphism of presheaves of commutative rings `Ï† : S âŸ¶ R`,
this is the type of relative `Ï†`-derivation of a presheaf of `R`-modules `M`. -/
abbrev Derivation' : Type _ := M.Derivation (F := ğŸ­ D) Ï†'

namespace Derivation'

@[simp]
nonrec lemma d_app (d : M.Derivation' Ï†') {X : Dáµ’áµ–} (a : S'.obj X) :
    d.d (Ï†'.app X a) = 0 :=
  d.d_app _

end Derivation'

namespace DifferentialsConstruction

/-- Auxiliary definition for `relativeDifferentials'`. -/
noncomputable def relativeDifferentials'BundledCore :
    BundledCorePresheafOfModules.{u} (R â‹™ forgetâ‚‚ _ _) where
  obj X := CommRingCat.KaehlerDifferential (Ï†'.app X)
  map f := CommRingCat.KaehlerDifferential.map (Ï†'.naturality f)

/-- The presheaf of relative differentials of a morphism of presheaves of
commutative rings. -/
noncomputable def relativeDifferentials' :
    PresheafOfModules.{u} (R â‹™ forgetâ‚‚ _ _) :=
  (relativeDifferentials'BundledCore Ï†').toPresheafOfModules

@[simp]
lemma relativeDifferentials'_obj (X : Dáµ’áµ–) :
    (relativeDifferentials' Ï†').obj X =
      CommRingCat.KaehlerDifferential (Ï†'.app X) := rfl

-- Note: this cannot be a simp lemma because `dsimp` would
-- simplify the composition of functors `R â‹™ forgetâ‚‚ _ _`
lemma relativeDifferentials'_map_apply {X Y : Dáµ’áµ–} (f : X âŸ¶ Y)
    (x : CommRingCat.KaehlerDifferential (Ï†'.app X)) :
    (relativeDifferentials' Ï†').map f x =
        CommRingCat.KaehlerDifferential.map (Ï†'.naturality f) x := rfl

/-- The universal derivation. -/
noncomputable def derivation' : (relativeDifferentials' Ï†').Derivation' Ï†' where
  d := AddMonoidHom.mk' (fun x => CommRingCat.KaehlerDifferential.d x) (by simp)
  d_map _ _ := by
    rw [relativeDifferentials'_map_apply, AddMonoidHom.mk'_apply,
      AddMonoidHom.mk'_apply, CommRingCat.KaehlerDifferential.map_d]

/-
TODO :

def isUniversal' : (derivation' Ï†').Universal := sorry

instance : HasDifferentials (F := ğŸ­ D) Ï†' := âŸ¨_, _,  âŸ¨isUniversal' Ï†'âŸ©âŸ©
-/

end DifferentialsConstruction

end PresheafOfModules
