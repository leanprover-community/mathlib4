/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Category.ModuleCat.ChangeOfRings
import Mathlib.Algebra.Category.Ring.Basic
import Mathlib.CategoryTheory.Bicategory.Functor.LocallyDiscrete
import Mathlib.CategoryTheory.Adjunction.Mates

/-!
# The pseudofunctors which send a commutative ring to its category of modules

In this file, we construct the pseudofunctors
`CommRingCat.moduleCatRestrictScalarsPseudofunctor` and
`RingCat.moduleCatRestrictScalarsPseudofunctor` which sends a (commutative) ring
to its category of modules: the contravariant functoriality is given
by the restriction of scalars functors.

We also define a pseudofunctor
`CommRingCat.moduleCatExtendScalarsPseudofunctor`: the covariant functoriality
is given by the extension of scalars functors.

-/

universe v u

open CategoryTheory ModuleCat

/-- The pseudofunctor from `LocallyDiscrete CommRingCat·µí·µñ` to `Cat` which sends
a commutative ring `R` to its category of modules. The functoriality is given by
the restriction of scalars. -/
@[simps! obj map mapId mapComp]
noncomputable def CommRingCat.moduleCatRestrictScalarsPseudofunctor :
    Pseudofunctor (LocallyDiscrete CommRingCat.{u}·µí·µñ) Cat :=
  LocallyDiscrete.mkPseudofunctor
    (fun R ‚Ü¶ Cat.of (ModuleCat.{v} R.unop))
    (fun f ‚Ü¶ restrictScalars f.unop)
    (fun R ‚Ü¶ restrictScalarsId R.unop)
    (fun f g ‚Ü¶ restrictScalarsComp g.unop f.unop)

/-- The pseudofunctor from `LocallyDiscrete RingCat·µí·µñ` to `Cat` which sends a ring `R`
to its category of modules. The functoriality is given by the restriction of scalars. -/
@[simps! obj map mapId mapComp]
noncomputable def RingCat.moduleCatRestrictScalarsPseudofunctor :
    Pseudofunctor (LocallyDiscrete CommRingCat.{u}·µí·µñ) Cat :=
  LocallyDiscrete.mkPseudofunctor
    (fun R ‚Ü¶ Cat.of (ModuleCat.{v} R.unop))
    (fun f ‚Ü¶ restrictScalars f.unop)
    (fun R ‚Ü¶ restrictScalarsId R.unop)
    (fun f g ‚Ü¶ restrictScalarsComp g.unop f.unop)

namespace ModuleCat
-- to be moved to ChangeOfRings
section

@[ext]
lemma extendScalars_hom_ext {R S : Type u} [CommRing R] [CommRing S]
    {f : R ‚Üí+* S} {M : ModuleCat R} {N : ModuleCat S}
    {Œ± Œ≤ : (extendScalars f).obj M ‚ü∂ N}
    (h : ‚àÄ (m : M), Œ± ((1 : S) ‚äó‚Çú m) = Œ≤ ((1 : S) ‚äó‚Çú m)) : Œ± = Œ≤ := by
  letI := f.toAlgebra
  apply (restrictScalars f).map_injective
  apply TensorProduct.ext'
  intro (s : S) m
  change Œ± (s ‚äó‚Çú m) = Œ≤ (s ‚äó‚Çú m)
  have : (s : S) ‚äó‚Çú[R] (m : M) = s ‚Ä¢ (1 : S) ‚äó‚Çú[R] m := by
    rw [ExtendScalars.smul_tmul, mul_one]
  simp only [this, map_smul, h]

end

section

variable (R : Type u) [CommRing R]

/-- The extension of scalars by the identity of a ring is isomorphic to the
identity functor. -/
noncomputable def extendScalarsId : extendScalars (RingHom.id R) ‚âÖ ùü≠ _ :=
  ((conjugateIsoEquiv (extendRestrictScalarsAdj (RingHom.id R)) Adjunction.id).symm
    (restrictScalarsId R)).symm

variable {R}

lemma extendScalarsId_inv_app_apply (M : ModuleCat R) (m : M):
    (extendScalarsId R).inv.app M m = (1 : R) ‚äó‚Çú m := rfl

lemma homEquiv_extendScalarsId (M : ModuleCat R) :
    (extendRestrictScalarsAdj (RingHom.id R)).homEquiv _ _ ((extendScalarsId R).hom.app M) =
      (restrictScalarsId R).inv.app M := by
  ext m
  rw [extendRestrictScalarsAdj_homEquiv_apply]
  rw [‚Üê extendScalarsId_inv_app_apply]
  erw [‚Üê comp_apply]
  simp

lemma extendScalarsId_hom_app_one_tmul (M : ModuleCat R) (m : M) :
    ((extendScalarsId R).hom.app M) ((1 : R) ‚äó‚Çú m) = m := by
  rw [‚Üê extendRestrictScalarsAdj_homEquiv_apply,
    homEquiv_extendScalarsId]
  dsimp

end

section

variable {R‚ÇÅ R‚ÇÇ R‚ÇÉ : Type u} [CommRing R‚ÇÅ] [CommRing R‚ÇÇ] [CommRing R‚ÇÉ]
  (f : R‚ÇÅ ‚Üí+* R‚ÇÇ) (g : R‚ÇÇ ‚Üí+* R‚ÇÉ)

/-- The extension of scalars by a composition of commutative ring morphisms
identify to the composition of the extension of scalars functors. -/
noncomputable def extendScalarsComp :
    extendScalars (g.comp f) ‚âÖ extendScalars f ‚ãô extendScalars g :=
  (conjugateIsoEquiv
    ((extendRestrictScalarsAdj f).comp (extendRestrictScalarsAdj g))
    (extendRestrictScalarsAdj (g.comp f))).symm (restrictScalarsComp f g).symm

lemma homEquiv_extendScalarsComp (M : ModuleCat R‚ÇÅ) :
    (extendRestrictScalarsAdj (g.comp f)).homEquiv _ _ ((extendScalarsComp f g).hom.app M) =
      (extendRestrictScalarsAdj f).unit.app M ‚â´
        (restrictScalars f).map ((extendRestrictScalarsAdj g).unit.app _) ‚â´
        (restrictScalarsComp f g).inv.app _ := by
  dsimp [extendScalarsComp, conjugateIsoEquiv, conjugateEquiv]
  simp only [Category.assoc, Category.id_comp, Category.comp_id,
    Adjunction.comp_unit_app, Adjunction.homEquiv_unit,
    Functor.map_comp, Adjunction.unit_naturality_assoc,
    Adjunction.right_triangle_components]
  rfl

lemma extendScalarsComp_hom_app_one_tmul (M : ModuleCat R‚ÇÅ) (m : M) :
    ((extendScalarsComp f g).hom.app M) ((1 : R‚ÇÉ) ‚äó‚Çú m) =
        by exact (1 : R‚ÇÉ) ‚äó‚Çú ((1 : R‚ÇÇ) ‚äó‚Çú m) := by
  rw [‚Üê extendRestrictScalarsAdj_homEquiv_apply, homEquiv_extendScalarsComp]
  rfl

end

section

variable {R‚ÇÄ R‚ÇÅ R‚ÇÇ R‚ÇÉ : Type u} [CommRing R‚ÇÄ] [CommRing R‚ÇÅ] [CommRing R‚ÇÇ] [CommRing R‚ÇÉ]
  (f : R‚ÇÄ ‚Üí+* R‚ÇÅ) (g : R‚ÇÅ ‚Üí+* R‚ÇÇ) (h : R‚ÇÇ ‚Üí+* R‚ÇÉ)

@[reassoc]
lemma extendScalars_assoc :
    (extendScalarsComp (g.comp f) h).hom ‚â´ whiskerRight (extendScalarsComp f g).hom _ =
      (extendScalarsComp f (h.comp g)).hom ‚â´ whiskerLeft _ (extendScalarsComp g h).hom ‚â´
        (Functor.associator _ _ _).inv := by
  ext M m
  dsimp
  have := extendScalarsComp_hom_app_one_tmul (g.comp f) h M m
  erw [this, extendScalarsComp_hom_app_one_tmul f (h.comp g) M m,
    extendScalarsComp_hom_app_one_tmul g h, ExtendScalars.map_tmul,
    extendScalarsComp_hom_app_one_tmul f g]

/-- The associativity compatibility for the extension of scalars, in the exact form
that is needed in the definition `ModuleCat.extendScalarsPseudofunctor`. -/
lemma extendScalars_assoc' :
    (extendScalarsComp (g.comp f) h).hom ‚â´ whiskerRight (extendScalarsComp f g).hom _ ‚â´
      (Functor.associator _ _ _).hom ‚â´ whiskerLeft _ (extendScalarsComp g h).inv ‚â´
        (extendScalarsComp f (h.comp g)).inv = ùüô _ := by
  rw [extendScalars_assoc_assoc]
  simp only [Iso.inv_hom_id_assoc, ‚Üê whiskerLeft_comp_assoc, Iso.hom_inv_id,
    whiskerLeft_id', Category.id_comp]

@[reassoc]
lemma extendScalars_id_comp :
    (extendScalarsComp (RingHom.id R‚ÇÄ) f).hom ‚â´ whiskerRight (extendScalarsId R‚ÇÄ).hom _ ‚â´
      (Functor.leftUnitor _).hom = ùüô _ := by
  ext M m
  dsimp
  erw [extendScalarsComp_hom_app_one_tmul (RingHom.id R‚ÇÄ) f M m]
  rw [ExtendScalars.map_tmul]
  erw [extendScalarsId_hom_app_one_tmul]
  rfl

@[reassoc]
lemma extendScalars_comp_id :
    (extendScalarsComp f (RingHom.id R‚ÇÅ)).hom ‚â´ whiskerLeft _ (extendScalarsId R‚ÇÅ).hom ‚â´
      (Functor.rightUnitor _).hom = ùüô _ := by
  ext M m
  dsimp
  erw [extendScalarsComp_hom_app_one_tmul f (RingHom.id R‚ÇÅ) M m,
    extendScalarsId_hom_app_one_tmul]
  rfl

end

end ModuleCat

/-- The pseudofunctor from `LocallyDiscrete CommRingCat` to `Cat` which sends
a commutative ring `R` to its category of modules. The functoriality is given by
the extension of scalars. -/
@[simps! obj map mapId mapComp]
noncomputable def CommRingCat.moduleCatExtendScalarsPseudofunctor :
    Pseudofunctor (LocallyDiscrete CommRingCat.{u}) Cat :=
  LocallyDiscrete.mkPseudofunctor
    (fun R ‚Ü¶ Cat.of (ModuleCat.{u} R))
    (fun f ‚Ü¶ extendScalars f)
    (fun R ‚Ü¶ extendScalarsId R)
    (fun f g ‚Ü¶ extendScalarsComp f g)
    (fun _ _ _ ‚Ü¶ extendScalars_assoc' _ _ _)
    (fun _ ‚Ü¶ extendScalars_id_comp _)
    (fun _ ‚Ü¶ extendScalars_comp_id _)
