/-
Copyright (c) 2021 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Johan Commelin
-/
import Mathlib.Algebra.Category.ModuleCat.Monoidal.Basic
import Mathlib.CategoryTheory.Monoidal.Types.Basic
import Mathlib.LinearAlgebra.DirectSum.Finsupp
import Mathlib.CategoryTheory.Linear.LinearFunctor

/-!
The functor of forming finitely supported functions on a type with values in a `[Ring R]`
is the left adjoint of
the forgetful functor from `R`-modules to types.
-/


assert_not_exists Cardinal

noncomputable section

open CategoryTheory

namespace ModuleCat

universe u

variable (R : Type u)

section

variable [Ring R]

/-- The free functor `Type u â¥¤ ModuleCat R` sending a type `X` to the
free `R`-module with generators `x : X`, implemented as the type `X â†’â‚€ R`.
-/
def free : Type u â¥¤ ModuleCat R where
  obj X := ModuleCat.of R (X â†’â‚€ R)
  map {_ _} f := ofHom <| Finsupp.lmapDomain _ _ f
  map_id := by intros; ext : 1; exact Finsupp.lmapDomain_id _ _
  map_comp := by intros; ext : 1; exact Finsupp.lmapDomain_comp _ _ _ _

variable {R}

/-- Constructor for elements in the module `(free R).obj X`. -/
noncomputable def freeMk {X : Type u} (x : X) : (free R).obj X := Finsupp.single x 1

@[ext 1200]
lemma free_hom_ext {X : Type u} {M : ModuleCat.{u} R} {f g : (free R).obj X âŸ¶ M}
    (h : âˆ€ (x : X), f (freeMk x) = g (freeMk x)) :
    f = g :=
  ModuleCat.hom_ext (Finsupp.lhom_ext' (fun x â†¦ LinearMap.ext_ring (h x)))

/-- The morphism of modules `(free R).obj X âŸ¶ M` corresponding
to a map `f : X âŸ¶ M`. -/
noncomputable def freeDesc {X : Type u} {M : ModuleCat.{u} R} (f : X âŸ¶ M) :
    (free R).obj X âŸ¶ M :=
  ofHom <| Finsupp.lift M R X f

@[simp]
lemma freeDesc_apply {X : Type u} {M : ModuleCat.{u} R} (f : X âŸ¶ M) (x : X) :
    freeDesc f (freeMk x) = f x := by
  dsimp [freeDesc]
  erw [Finsupp.lift_apply, Finsupp.sum_single_index]
  all_goals simp

@[simp]
lemma free_map_apply {X Y : Type u} (f : X â†’ Y) (x : X) :
    (free R).map f (freeMk x) = freeMk (f x) := by
  apply Finsupp.mapDomain_single

/-- The bijection `((free R).obj X âŸ¶ M) â‰ƒ (X â†’ M)` when `X` is a type and `M` a module. -/
@[simps]
def freeHomEquiv {X : Type u} {M : ModuleCat.{u} R} :
    ((free R).obj X âŸ¶ M) â‰ƒ (X â†’ M) where
  toFun Ï† x := Ï† (freeMk x)
  invFun Ïˆ := freeDesc Ïˆ
  left_inv _ := by ext; simp
  right_inv _ := by ext; simp

variable (R)

/-- The free-forgetful adjunction for R-modules.
-/
def adj : free R âŠ£ forget (ModuleCat.{u} R) :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun _ _ => freeHomEquiv
      homEquiv_naturality_left_symm := fun {X Y M} f g â†¦ by ext; simp [freeHomEquiv] }

@[simp]
lemma adj_homEquiv (X : Type u) (M : ModuleCat.{u} R) :
    (adj R).homEquiv X M = freeHomEquiv := by
  simp only [adj, Adjunction.mkOfHomEquiv_homEquiv]

instance : (forget (ModuleCat.{u} R)).IsRightAdjoint  :=
  (adj R).isRightAdjoint

end

section Free

open MonoidalCategory

variable [CommRing R]

namespace FreeMonoidal

/-- The canonical isomorphism `ğŸ™_ (ModuleCat R) â‰… (free R).obj (ğŸ™_ (Type u))`.
(This should not be used directly: it is part of the implementation of the
monoidal structure on the functor `free R`.) -/
def ÎµIso : ğŸ™_ (ModuleCat R) â‰… (free R).obj (ğŸ™_ (Type u)) where
  hom := ofHom <| Finsupp.lsingle PUnit.unit
  inv := ofHom <| Finsupp.lapply PUnit.unit
  hom_inv_id := by
    ext
    simp [free]
  inv_hom_id := by
    ext âŸ¨âŸ©
    dsimp [freeMk]
    erw [Finsupp.lapply_apply, Finsupp.lsingle_apply]
    rw [Finsupp.single_eq_same]

@[simp]
lemma ÎµIso_hom_one : (ÎµIso R).hom 1 = freeMk PUnit.unit := rfl

@[simp]
lemma ÎµIso_inv_freeMk (x : PUnit) : (ÎµIso R).inv (freeMk x) = 1 := by
  dsimp [ÎµIso, freeMk]
  erw [Finsupp.lapply_apply]
  rw [Finsupp.single_eq_same]

/-- The canonical isomorphism `(free R).obj X âŠ— (free R).obj Y â‰… (free R).obj (X âŠ— Y)`
for two types `X` and `Y`.
(This should not be used directly: it is is part of the implementation of the
monoidal structure on the functor `free R`.) -/
def Î¼Iso (X Y : Type u) :
    (free R).obj X âŠ— (free R).obj Y â‰… (free R).obj (X âŠ— Y) :=
  (finsuppTensorFinsupp' R _ _).toModuleIso

@[simp]
lemma Î¼Iso_hom_freeMk_tmul_freeMk {X Y : Type u} (x : X) (y : Y) :
    (Î¼Iso R X Y).hom (freeMk x âŠ—â‚œ freeMk y) = freeMk âŸ¨x, yâŸ© := by
  dsimp [Î¼Iso, freeMk]
  erw [finsuppTensorFinsupp'_single_tmul_single]
  rw [mul_one]

@[simp]
lemma Î¼Iso_inv_freeMk {X Y : Type u} (z : X âŠ— Y) :
    (Î¼Iso R X Y).inv (freeMk z) = freeMk z.1 âŠ—â‚œ freeMk z.2 := by
  dsimp [Î¼Iso, freeMk]
  erw [finsuppTensorFinsupp'_symm_single_eq_single_one_tmul]

end FreeMonoidal

open FreeMonoidal in
/-- The free functor `Type u â¥¤ ModuleCat R` is a monoidal functor. -/
instance : (free R).Monoidal :=
  Functor.CoreMonoidal.toMonoidal
    { ÎµIso := ÎµIso R
      Î¼Iso := Î¼Iso R
      Î¼Iso_hom_natural_left := fun {X Y} f X' â†¦ by
        rw [â† cancel_epi (Î¼Iso R X X').inv]
        aesop
      Î¼Iso_hom_natural_right := fun {X Y} X' f â†¦ by
        rw [â† cancel_epi (Î¼Iso R X' X).inv]
        aesop
      associativity := fun X Y Z â†¦ by
        rw [â† cancel_epi ((Î¼Iso R X Y).inv â–· _), â† cancel_epi (Î¼Iso R _ _).inv]
        ext âŸ¨âŸ¨x, yâŸ©, zâŸ©
        dsimp
        rw [Î¼Iso_inv_freeMk, MonoidalCategory.whiskerRight_apply, Î¼Iso_inv_freeMk,
          MonoidalCategory.whiskerRight_apply, Î¼Iso_hom_freeMk_tmul_freeMk,
          Î¼Iso_hom_freeMk_tmul_freeMk, free_map_apply,
          CategoryTheory.associator_hom_apply, MonoidalCategory.associator_hom_apply,
          MonoidalCategory.whiskerLeft_apply, Î¼Iso_hom_freeMk_tmul_freeMk,
          Î¼Iso_hom_freeMk_tmul_freeMk]
      left_unitality := fun X â†¦ by
        rw [â† cancel_epi (Î»_ _).inv, Iso.inv_hom_id]
        aesop
      right_unitality := fun X â†¦ by
        rw [â† cancel_epi (Ï_ _).inv, Iso.inv_hom_id]
        aesop }

open Functor.LaxMonoidal Functor.OplaxMonoidal

@[simp]
lemma free_Îµ_one : Îµ (free R) 1 = freeMk PUnit.unit := rfl

@[simp]
lemma free_Î·_freeMk (x : PUnit) : Î· (free R) (freeMk x) = 1 := by
  apply FreeMonoidal.ÎµIso_inv_freeMk

@[simp]
lemma free_Î¼_freeMk_tmul_freeMk {X Y : Type u} (x : X) (y : Y) :
    Î¼ (free R) _ _ (freeMk x âŠ—â‚œ freeMk y) = freeMk âŸ¨x, yâŸ© := by
  apply FreeMonoidal.Î¼Iso_hom_freeMk_tmul_freeMk

@[simp]
lemma free_Î´_freeMk {X Y : Type u} (z : X âŠ— Y) :
    Î´ (free R) _ _ (freeMk z) = freeMk z.1 âŠ—â‚œ freeMk z.2 := by
  apply FreeMonoidal.Î¼Iso_inv_freeMk

end Free

end ModuleCat

namespace CategoryTheory

universe v u

/-- `Free R C` is a type synonym for `C`, which, given `[CommRing R]` and `[Category C]`,
we will equip with a category structure where the morphisms are formal `R`-linear combinations
of the morphisms in `C`.
-/
-- Porting note (https://github.com/leanprover-community/mathlib4/pull/5171): Removed has_nonempty_instance nolint; linter not ported yet
@[nolint unusedArguments]
def Free (_ : Type*) (C : Type u) :=
  C

/-- Consider an object of `C` as an object of the `R`-linear completion.

It may be preferable to use `(Free.embedding R C).obj X` instead;
this functor can also be used to lift morphisms.
-/
def Free.of (R : Type*) {C : Type u} (X : C) : Free R C :=
  X

variable (R : Type*) [CommRing R] (C : Type u) [Category.{v} C]

open Finsupp

-- Conceptually, it would be nice to construct this via "transport of enrichment",
-- using the fact that `ModuleCat.Free R : Type â¥¤ ModuleCat R` and `ModuleCat.forget` are both lax
-- monoidal. This still seems difficult, so we just do it by hand.
instance categoryFree : Category (Free R C) where
  Hom := fun X Y : C => (X âŸ¶ Y) â†’â‚€ R
  id := fun X : C => Finsupp.single (ğŸ™ X) 1
  comp {X _ Z : C} f g :=
    (f.sum (fun f' s => g.sum (fun g' t => Finsupp.single (f' â‰« g') (s * t))) : (X âŸ¶ Z) â†’â‚€ R)
  assoc {W X Y Z} f g h := by
    dsimp
    -- This imitates the proof of associativity for `MonoidAlgebra`.
    simp only [sum_sum_index, sum_single_index, single_zero, single_add, eq_self_iff_true,
      forall_true_iff, forallâ‚ƒ_true_iff, add_mul, mul_add, Category.assoc, mul_assoc,
      zero_mul, mul_zero, sum_zero, sum_add]

namespace Free

section

-- Porting note: removed local reducible attribute for categoryFree, adjusted dsimp invocations
-- accordingly

instance : Preadditive (Free R C) where
  homGroup _ _ := Finsupp.instAddCommGroup
  add_comp X Y Z f f' g := by
    dsimp [CategoryTheory.categoryFree]
    rw [Finsupp.sum_add_index'] <;> Â· simp [add_mul]
  comp_add X Y Z f g g' := by
    dsimp [CategoryTheory.categoryFree]
    rw [â† Finsupp.sum_add]
    congr; ext r h
    rw [Finsupp.sum_add_index'] <;> Â· simp [mul_add]

instance : Linear R (Free R C) where
  homModule _ _ := Finsupp.module _ R
  smul_comp X Y Z r f g := by
    dsimp [CategoryTheory.categoryFree]
    rw [Finsupp.sum_smul_index] <;> simp [Finsupp.smul_sum, mul_assoc]
  comp_smul X Y Z f r g := by
    dsimp [CategoryTheory.categoryFree]
    simp_rw [Finsupp.smul_sum]
    congr; ext h s
    rw [Finsupp.sum_smul_index] <;> simp [Finsupp.smul_sum, mul_left_comm]

theorem single_comp_single {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (r s : R) :
    (single f r â‰« single g s : Free.of R X âŸ¶ Free.of R Z) = single (f â‰« g) (r * s) := by
  dsimp [CategoryTheory.categoryFree]; simp

end

attribute [local simp] single_comp_single

/-- A category embeds into its `R`-linear completion.
-/
@[simps]
def embedding : C â¥¤ Free R C where
  obj X := X
  map {_ _} f := Finsupp.single f 1
  map_id _ := rfl
  map_comp {X Y Z} f g := by
    -- Porting note (https://github.com/leanprover-community/mathlib4/pull/10959): simp used to be able to close this goal
    dsimp only []
    rw [single_comp_single, one_mul]

variable {C} {D : Type u} [Category.{v} D] [Preadditive D] [Linear R D]

open Preadditive Linear

/-- A functor to an `R`-linear category lifts to a functor from its `R`-linear completion.
-/
@[simps]
def lift (F : C â¥¤ D) : Free R C â¥¤ D where
  obj X := F.obj X
  map {_ _} f := f.sum fun f' r => r â€¢ F.map f'
  map_id := by dsimp [CategoryTheory.categoryFree]; simp
  map_comp {X Y Z} f g := by
    apply Finsupp.induction_linear f
    Â· simp
    Â· intro fâ‚ fâ‚‚ wâ‚ wâ‚‚
      rw [add_comp]
      dsimp at *
      rw [Finsupp.sum_add_index', Finsupp.sum_add_index']
      Â· simp only [wâ‚, wâ‚‚, add_comp]
      Â· intros; rw [zero_smul]
      Â· intros; simp only [add_smul]
      Â· intros; rw [zero_smul]
      Â· intros; simp only [add_smul]
    Â· intro f' r
      apply Finsupp.induction_linear g
      Â· simp
      Â· intro fâ‚ fâ‚‚ wâ‚ wâ‚‚
        rw [comp_add]
        dsimp at *
        rw [Finsupp.sum_add_index', Finsupp.sum_add_index']
        Â· simp only [wâ‚, wâ‚‚, comp_add]
        Â· intros; rw [zero_smul]
        Â· intros; simp only [add_smul]
        Â· intros; rw [zero_smul]
        Â· intros; simp only [add_smul]
      Â· intro g' s
        rw [single_comp_single _ _ f' g' r s]
        simp [mul_comm r s, mul_smul]

theorem lift_map_single (F : C â¥¤ D) {X Y : C} (f : X âŸ¶ Y) (r : R) :
    (lift R F).map (single f r) = r â€¢ F.map f := by simp

instance lift_additive (F : C â¥¤ D) : (lift R F).Additive where
  map_add {X Y} f g := by
    dsimp
    rw [Finsupp.sum_add_index'] <;> simp [add_smul]

instance lift_linear (F : C â¥¤ D) : (lift R F).Linear R where
  map_smul {X Y} f r := by
    dsimp
    rw [Finsupp.sum_smul_index] <;> simp [Finsupp.smul_sum, mul_smul]

/-- The embedding into the `R`-linear completion, followed by the lift,
is isomorphic to the original functor.
-/
def embeddingLiftIso (F : C â¥¤ D) : embedding R C â‹™ lift R F â‰… F :=
  NatIso.ofComponents fun _ => Iso.refl _

/-- Two `R`-linear functors out of the `R`-linear completion are isomorphic iff their
compositions with the embedding functor are isomorphic.
-/
def ext {F G : Free R C â¥¤ D} [F.Additive] [F.Linear R] [G.Additive] [G.Linear R]
    (Î± : embedding R C â‹™ F â‰… embedding R C â‹™ G) : F â‰… G :=
  NatIso.ofComponents (fun X => Î±.app X)
    (by
      intro X Y f
      apply Finsupp.induction_linear f
      Â· simp
      Â· intro fâ‚ fâ‚‚ wâ‚ wâ‚‚
        -- Porting note: Using rw instead of simp
        rw [Functor.map_add, add_comp, wâ‚, wâ‚‚, Functor.map_add, comp_add]
      Â· intro f' r
        rw [Iso.app_hom, Iso.app_hom, â† smul_single_one, F.map_smul, G.map_smul, smul_comp,
          comp_smul]
        change r â€¢ (embedding R C â‹™ F).map f' â‰« _ = r â€¢ _ â‰« (embedding R C â‹™ G).map f'
        rw [Î±.hom.naturality f'])

/-- `Free.lift` is unique amongst `R`-linear functors `Free R C â¥¤ D`
which compose with `embedding â„¤ C` to give the original functor.
-/
def liftUnique (F : C â¥¤ D) (L : Free R C â¥¤ D) [L.Additive] [L.Linear R]
    (Î± : embedding R C â‹™ L â‰… F) : L â‰… lift R F :=
  ext R (Î±.trans (embeddingLiftIso R F).symm)

end Free
end CategoryTheory
