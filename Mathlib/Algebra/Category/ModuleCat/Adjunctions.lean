/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Johan Commelin
-/
import Mathlib.Algebra.Category.ModuleCat.Monoidal.Basic
import Mathlib.CategoryTheory.Monoidal.Functorial
import Mathlib.CategoryTheory.Monoidal.Types.Basic
import Mathlib.LinearAlgebra.DirectSum.Finsupp
import Mathlib.CategoryTheory.Linear.LinearFunctor

#align_import algebra.category.Module.adjunctions from "leanprover-community/mathlib"@"95a87616d63b3cb49d3fe678d416fbe9c4217bf4"

/-!
The functor of forming finitely supported functions on a type with values in a `[Ring R]`
is the left adjoint of
the forgetful functor from `R`-modules to types.
-/

set_option linter.uppercaseLean3 false -- `Module`

noncomputable section

open CategoryTheory

namespace ModuleCat

universe u

open scoped Classical

variable (R : Type u)

section

variable [Ring R]

/-- The free functor `Type u â¥¤ ModuleCat R` sending a type `X` to the
free `R`-module with generators `x : X`, implemented as the type `X â†’â‚€ R`.
-/
@[simps]
def free : Type u â¥¤ ModuleCat R where
  obj X := ModuleCat.of R (X â†’â‚€ R)
  map {X Y} f := Finsupp.lmapDomain _ _ f
  map_id := by intros; exact Finsupp.lmapDomain_id _ _
  map_comp := by intros; exact Finsupp.lmapDomain_comp _ _ _ _
#align Module.free ModuleCat.free

/-- The free-forgetful adjunction for R-modules.
-/
def adj : free R âŠ£ forget (ModuleCat.{u} R) :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun X M => (Finsupp.lift M R X).toEquiv.symm
      homEquiv_naturality_left_symm := fun {_ _} M f g =>
        Finsupp.lhom_ext' fun x =>
          LinearMap.ext_ring
            (Finsupp.sum_mapDomain_index_addMonoidHom fun y => (smulAddHom R M).flip (g y)).symm }
#align Module.adj ModuleCat.adj

instance : (forget (ModuleCat.{u} R)).IsRightAdjoint  :=
  (adj R).isRightAdjoint

end

namespace Free

open MonoidalCategory

variable [CommRing R]

attribute [local ext] TensorProduct.ext

/-- (Implementation detail) The unitor for `Free R`. -/
def Îµ : ğŸ™_ (ModuleCat.{u} R) âŸ¶ (free R).obj (ğŸ™_ (Type u)) :=
  Finsupp.lsingle PUnit.unit
#align Module.free.Îµ ModuleCat.Free.Îµ

-- This lemma has always been bad, but lean4#2644 made `simp` start noticing
@[simp, nolint simpNF]
theorem Îµ_apply (r : R) : Îµ R r = Finsupp.single PUnit.unit r :=
  rfl
#align Module.free.Îµ_apply ModuleCat.Free.Îµ_apply

/-- (Implementation detail) The tensorator for `Free R`. -/
def Î¼ (Î± Î² : Type u) : (free R).obj Î± âŠ— (free R).obj Î² â‰… (free R).obj (Î± âŠ— Î²) :=
  (finsuppTensorFinsupp' R Î± Î²).toModuleIso
#align Module.free.Î¼ ModuleCat.Free.Î¼

theorem Î¼_natural {X Y X' Y' : Type u} (f : X âŸ¶ Y) (g : X' âŸ¶ Y') :
    ((free R).map f âŠ— (free R).map g) â‰« (Î¼ R Y Y').hom = (Î¼ R X X').hom â‰« (free R).map (f âŠ— g) := by
  -- Porting note (#11041): broken ext
  apply TensorProduct.ext
  apply Finsupp.lhom_ext'
  intro x
  apply LinearMap.ext_ring
  apply Finsupp.lhom_ext'
  intro x'
  apply LinearMap.ext_ring
  apply Finsupp.ext
  intro âŸ¨y, y'âŸ©
  -- Porting note (#10934): used to be dsimp [Î¼]
  change (finsuppTensorFinsupp' R Y Y')
    (Finsupp.mapDomain f (Finsupp.single x 1) âŠ—â‚œ[R] Finsupp.mapDomain g (Finsupp.single x' 1)) _
    = (Finsupp.mapDomain (f âŠ— g) (finsuppTensorFinsupp' R X X'
    (Finsupp.single x 1 âŠ—â‚œ[R] Finsupp.single x' 1))) _

  -- extra `rfl` after leanprover/lean4#2466
  simp_rw [Finsupp.mapDomain_single, finsuppTensorFinsupp'_single_tmul_single, mul_one,
    Finsupp.mapDomain_single, CategoryTheory.tensor_apply]; rfl
#align Module.free.Î¼_natural ModuleCat.Free.Î¼_natural

theorem left_unitality (X : Type u) :
    (Î»_ ((free R).obj X)).hom =
      (Îµ R âŠ— ğŸ™ ((free R).obj X)) â‰« (Î¼ R (ğŸ™_ (Type u)) X).hom â‰« map (free R).obj (Î»_ X).hom := by
  -- Porting note (#11041): broken ext
  apply TensorProduct.ext
  apply LinearMap.ext_ring
  apply Finsupp.lhom_ext'
  intro x
  apply LinearMap.ext_ring
  apply Finsupp.ext
  intro x'
  -- Porting note (#10934): used to be dsimp [Îµ, Î¼]
  let q : X â†’â‚€ R := ((Î»_ (of R (X â†’â‚€ R))).hom) (1 âŠ—â‚œ[R] Finsupp.single x 1)
  change q x' = Finsupp.mapDomain (Î»_ X).hom (finsuppTensorFinsupp' R (ğŸ™_ (Type u)) X
    (Finsupp.single PUnit.unit 1 âŠ—â‚œ[R] Finsupp.single x 1)) x'
  simp_rw [q, finsuppTensorFinsupp'_single_tmul_single,
    ModuleCat.MonoidalCategory.leftUnitor_hom_apply, mul_one,
    Finsupp.mapDomain_single, CategoryTheory.leftUnitor_hom_apply, one_smul]
#align Module.free.left_unitality ModuleCat.Free.left_unitality

theorem right_unitality (X : Type u) :
    (Ï_ ((free R).obj X)).hom =
      (ğŸ™ ((free R).obj X) âŠ— Îµ R) â‰« (Î¼ R X (ğŸ™_ (Type u))).hom â‰« map (free R).obj (Ï_ X).hom := by
  -- Porting note (#11041): broken ext
  apply TensorProduct.ext
  apply Finsupp.lhom_ext'
  intro x
  apply LinearMap.ext_ring
  apply LinearMap.ext_ring
  apply Finsupp.ext
  intro x'
  -- Porting note (#10934): used to be dsimp [Îµ, Î¼]
  let q : X â†’â‚€ R := ((Ï_ (of R (X â†’â‚€ R))).hom) (Finsupp.single x 1 âŠ—â‚œ[R] 1)
  change q x' = Finsupp.mapDomain (Ï_ X).hom (finsuppTensorFinsupp' R X (ğŸ™_ (Type u))
    (Finsupp.single x 1 âŠ—â‚œ[R] Finsupp.single PUnit.unit 1)) x'
  simp_rw [q, finsuppTensorFinsupp'_single_tmul_single,
    ModuleCat.MonoidalCategory.rightUnitor_hom_apply, mul_one,
    Finsupp.mapDomain_single, CategoryTheory.rightUnitor_hom_apply, one_smul]
#align Module.free.right_unitality ModuleCat.Free.right_unitality

theorem associativity (X Y Z : Type u) :
    ((Î¼ R X Y).hom âŠ— ğŸ™ ((free R).obj Z)) â‰« (Î¼ R (X âŠ— Y) Z).hom â‰« map (free R).obj (Î±_ X Y Z).hom =
      (Î±_ ((free R).obj X) ((free R).obj Y) ((free R).obj Z)).hom â‰«
        (ğŸ™ ((free R).obj X) âŠ— (Î¼ R Y Z).hom) â‰« (Î¼ R X (Y âŠ— Z)).hom := by
  -- Porting note (#11041): broken ext
  apply TensorProduct.ext
  apply TensorProduct.ext
  apply Finsupp.lhom_ext'
  intro x
  apply LinearMap.ext_ring
  apply Finsupp.lhom_ext'
  intro y
  apply LinearMap.ext_ring
  apply Finsupp.lhom_ext'
  intro z
  apply LinearMap.ext_ring
  apply Finsupp.ext
  intro a
  -- Porting note (#10934): used to be dsimp [Î¼]
  change Finsupp.mapDomain (Î±_ X Y Z).hom (finsuppTensorFinsupp' R (X âŠ— Y) Z
    (finsuppTensorFinsupp' R X Y
    (Finsupp.single x 1 âŠ—â‚œ[R] Finsupp.single y 1) âŠ—â‚œ[R] Finsupp.single z 1)) a =
    finsuppTensorFinsupp' R X (Y âŠ— Z)
    (Finsupp.single x 1 âŠ—â‚œ[R]
      finsuppTensorFinsupp' R Y Z (Finsupp.single y 1 âŠ—â‚œ[R] Finsupp.single z 1)) a
  -- extra `rfl` after leanprover/lean4#2466
  simp_rw [finsuppTensorFinsupp'_single_tmul_single, Finsupp.mapDomain_single, mul_one,
    CategoryTheory.associator_hom_apply]; rfl
#align Module.free.associativity ModuleCat.Free.associativity

-- In fact, it's strong monoidal, but we don't yet have a typeclass for that.
/-- The free R-module functor is lax monoidal. -/
@[simps]
instance : LaxMonoidal.{u} (free R).obj := .ofTensorHom
  -- Send `R` to `PUnit â†’â‚€ R`
  (Îµ := Îµ R)
  -- Send `(Î± â†’â‚€ R) âŠ— (Î² â†’â‚€ R)` to `Î± Ã— Î² â†’â‚€ R`
  (Î¼ := fun X Y => (Î¼ R X Y).hom)
  (Î¼_natural := fun {_} {_} {_} {_} f g â†¦ Î¼_natural R f g)
  (left_unitality := left_unitality R)
  (right_unitality := right_unitality R)
  (associativity := associativity R)

instance : IsIso (@LaxMonoidal.Îµ _ _ _ _ _ _ (free R).obj _ _) := by
  refine âŸ¨âŸ¨Finsupp.lapply PUnit.unit, âŸ¨?_, ?_âŸ©âŸ©âŸ©
  Â· -- Porting note (#11041): broken ext
    apply LinearMap.ext_ring
    -- Porting note (#10959): simp used to be able to close this goal
    dsimp
    erw [ModuleCat.comp_def, LinearMap.comp_apply, Îµ_apply, Finsupp.lapply_apply,
      Finsupp.single_eq_same, id_apply]
  Â· -- Porting note (#11041): broken ext
    apply Finsupp.lhom_ext'
    intro âŸ¨âŸ©
    apply LinearMap.ext_ring
    apply Finsupp.ext
    intro âŸ¨âŸ©
    -- Porting note (#10959): simp used to be able to close this goal
    dsimp
    erw [ModuleCat.comp_def, LinearMap.comp_apply, Îµ_apply, Finsupp.lapply_apply,
      Finsupp.single_eq_same]

end Free

open MonoidalCategory

variable [CommRing R]

/-- The free functor `Type u â¥¤ ModuleCat R`, as a monoidal functor. -/
def monoidalFree : MonoidalFunctor (Type u) (ModuleCat.{u} R) :=
  { LaxMonoidalFunctor.of (free R).obj with
    -- Porting note (#10934): used to be dsimp
    Îµ_isIso := (by infer_instance : IsIso (@LaxMonoidal.Îµ _ _ _ _ _ _ (free R).obj _ _))
    Î¼_isIso := fun X Y => by dsimp; infer_instance }
#align Module.monoidal_free ModuleCat.monoidalFree

example (X Y : Type u) : (free R).obj (X Ã— Y) â‰… (free R).obj X âŠ— (free R).obj Y :=
  ((monoidalFree R).Î¼Iso X Y).symm

end ModuleCat

namespace CategoryTheory

universe v u

/-- `Free R C` is a type synonym for `C`, which, given `[CommRing R]` and `[Category C]`,
we will equip with a category structure where the morphisms are formal `R`-linear combinations
of the morphisms in `C`.
-/
-- Porting note(#5171): Removed has_nonempty_instance nolint; linter not ported yet
@[nolint unusedArguments]
def Free (_ : Type*) (C : Type u) :=
  C
#align category_theory.Free CategoryTheory.Free

/-- Consider an object of `C` as an object of the `R`-linear completion.

It may be preferable to use `(Free.embedding R C).obj X` instead;
this functor can also be used to lift morphisms.
-/
def Free.of (R : Type*) {C : Type u} (X : C) : Free R C :=
  X
#align category_theory.Free.of CategoryTheory.Free.of

variable (R : Type*) [CommRing R] (C : Type u) [Category.{v} C]

open Finsupp

-- Conceptually, it would be nice to construct this via "transport of enrichment",
-- using the fact that `ModuleCat.Free R : Type â¥¤ ModuleCat R` and `ModuleCat.forget` are both lax
-- monoidal. This still seems difficult, so we just do it by hand.
instance categoryFree : Category (Free R C) where
  Hom := fun X Y : C => (X âŸ¶ Y) â†’â‚€ R
  id := fun X : C => Finsupp.single (ğŸ™ X) 1
  comp {X Y Z : C} f g :=
    (f.sum (fun f' s => g.sum (fun g' t => Finsupp.single (f' â‰« g') (s * t))) : (X âŸ¶ Z) â†’â‚€ R)
  assoc {W X Y Z} f g h := by
    dsimp
    -- This imitates the proof of associativity for `MonoidAlgebra`.
    simp only [sum_sum_index, sum_single_index, single_zero, single_add, eq_self_iff_true,
      forall_true_iff, forallâ‚ƒ_true_iff, add_mul, mul_add, Category.assoc, mul_assoc,
      zero_mul, mul_zero, sum_zero, sum_add]
#align category_theory.category_Free CategoryTheory.categoryFree

namespace Free

section

-- Porting note: removed local reducible attribute for categoryFree, adjusted dsimp invocations
-- accordingly

instance : Preadditive (Free R C) where
  homGroup X Y := Finsupp.instAddCommGroup
  add_comp X Y Z f f' g := by
    dsimp [CategoryTheory.categoryFree]
    rw [Finsupp.sum_add_index'] <;> Â· simp [add_mul]
  comp_add X Y Z f g g' := by
    dsimp [CategoryTheory.categoryFree]
    rw [â† Finsupp.sum_add]
    congr; ext r h
    rw [Finsupp.sum_add_index'] <;> Â· simp [mul_add]

instance : Linear R (Free R C) where
  homModule X Y := Finsupp.module _ R
  smul_comp X Y Z r f g := by
    dsimp [CategoryTheory.categoryFree]
    rw [Finsupp.sum_smul_index] <;> simp [Finsupp.smul_sum, mul_assoc]
  comp_smul X Y Z f r g := by
    dsimp [CategoryTheory.categoryFree]
    simp_rw [Finsupp.smul_sum]
    congr; ext h s
    rw [Finsupp.sum_smul_index] <;> simp [Finsupp.smul_sum, mul_left_comm]

theorem single_comp_single {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (r s : R) :
    (single f r â‰« single g s : Free.of R X âŸ¶ Free.of R Z) = single (f â‰« g) (r * s) := by
  dsimp [CategoryTheory.categoryFree]; simp
#align category_theory.Free.single_comp_single CategoryTheory.Free.single_comp_single

end

attribute [local simp] single_comp_single

/-- A category embeds into its `R`-linear completion.
-/
@[simps]
def embedding : C â¥¤ Free R C where
  obj X := X
  map {X Y} f := Finsupp.single f 1
  map_id X := rfl
  map_comp {X Y Z} f g := by
    -- Porting note (#10959): simp used to be able to close this goal
    dsimp only []
    rw [single_comp_single, one_mul]
#align category_theory.Free.embedding CategoryTheory.Free.embedding

variable {C} {D : Type u} [Category.{v} D] [Preadditive D] [Linear R D]

open Preadditive Linear

/-- A functor to an `R`-linear category lifts to a functor from its `R`-linear completion.
-/
@[simps]
def lift (F : C â¥¤ D) : Free R C â¥¤ D where
  obj X := F.obj X
  map {X Y} f := f.sum fun f' r => r â€¢ F.map f'
  map_id := by dsimp [CategoryTheory.categoryFree]; simp
  map_comp {X Y Z} f g := by
    apply Finsupp.induction_linear f
    Â· -- Porting note (#10959): simp used to be able to close this goal
      dsimp
      rw [Limits.zero_comp, sum_zero_index, Limits.zero_comp]
    Â· intro fâ‚ fâ‚‚ wâ‚ wâ‚‚
      rw [add_comp]
      dsimp at *
      rw [Finsupp.sum_add_index', Finsupp.sum_add_index']
      Â· simp only [wâ‚, wâ‚‚, add_comp]
      Â· intros; rw [zero_smul]
      Â· intros; simp only [add_smul]
      Â· intros; rw [zero_smul]
      Â· intros; simp only [add_smul]
    Â· intro f' r
      apply Finsupp.induction_linear g
      Â· -- Porting note (#10959): simp used to be able to close this goal
        dsimp
        rw [Limits.comp_zero, sum_zero_index, Limits.comp_zero]
      Â· intro fâ‚ fâ‚‚ wâ‚ wâ‚‚
        rw [comp_add]
        dsimp at *
        rw [Finsupp.sum_add_index', Finsupp.sum_add_index']
        Â· simp only [wâ‚, wâ‚‚, comp_add]
        Â· intros; rw [zero_smul]
        Â· intros; simp only [add_smul]
        Â· intros; rw [zero_smul]
        Â· intros; simp only [add_smul]
      Â· intro g' s
        rw [single_comp_single _ _ f' g' r s]
        simp [mul_comm r s, mul_smul]
#align category_theory.Free.lift CategoryTheory.Free.lift

theorem lift_map_single (F : C â¥¤ D) {X Y : C} (f : X âŸ¶ Y) (r : R) :
    (lift R F).map (single f r) = r â€¢ F.map f := by simp
#align category_theory.Free.lift_map_single CategoryTheory.Free.lift_map_single

instance lift_additive (F : C â¥¤ D) : (lift R F).Additive where
  map_add {X Y} f g := by
    dsimp
    rw [Finsupp.sum_add_index'] <;> simp [add_smul]
#align category_theory.Free.lift_additive CategoryTheory.Free.lift_additive

instance lift_linear (F : C â¥¤ D) : (lift R F).Linear R where
  map_smul {X Y} f r := by
    dsimp
    rw [Finsupp.sum_smul_index] <;> simp [Finsupp.smul_sum, mul_smul]
#align category_theory.Free.lift_linear CategoryTheory.Free.lift_linear

/-- The embedding into the `R`-linear completion, followed by the lift,
is isomorphic to the original functor.
-/
def embeddingLiftIso (F : C â¥¤ D) : embedding R C â‹™ lift R F â‰… F :=
  NatIso.ofComponents fun X => Iso.refl _
#align category_theory.Free.embedding_lift_iso CategoryTheory.Free.embeddingLiftIso

/-- Two `R`-linear functors out of the `R`-linear completion are isomorphic iff their
compositions with the embedding functor are isomorphic.
-/
-- Porting note: used to be @[ext]
def ext {F G : Free R C â¥¤ D} [F.Additive] [F.Linear R] [G.Additive] [G.Linear R]
    (Î± : embedding R C â‹™ F â‰… embedding R C â‹™ G) : F â‰… G :=
  NatIso.ofComponents (fun X => Î±.app X)
    (by
      intro X Y f
      apply Finsupp.induction_linear f
      Â· -- Porting note (#10959): simp used to be able to close this goal
        rw [Functor.map_zero, Limits.zero_comp, Functor.map_zero, Limits.comp_zero]
      Â· intro fâ‚ fâ‚‚ wâ‚ wâ‚‚
        -- Porting note: Using rw instead of simp
        rw [Functor.map_add, add_comp, wâ‚, wâ‚‚, Functor.map_add, comp_add]
      Â· intro f' r
        rw [Iso.app_hom, Iso.app_hom, â† smul_single_one, F.map_smul, G.map_smul, smul_comp,
          comp_smul]
        change r â€¢ (embedding R C â‹™ F).map f' â‰« _ = r â€¢ _ â‰« (embedding R C â‹™ G).map f'
        rw [Î±.hom.naturality f'])
#align category_theory.Free.ext CategoryTheory.Free.ext

/-- `Free.lift` is unique amongst `R`-linear functors `Free R C â¥¤ D`
which compose with `embedding â„¤ C` to give the original functor.
-/
def liftUnique (F : C â¥¤ D) (L : Free R C â¥¤ D) [L.Additive] [L.Linear R]
    (Î± : embedding R C â‹™ L â‰… F) : L â‰… lift R F :=
  ext R (Î±.trans (embeddingLiftIso R F).symm)
#align category_theory.Free.lift_unique CategoryTheory.Free.liftUnique

end Free
end CategoryTheory
