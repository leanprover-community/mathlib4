/-
Copyright (c) 2021 Justus Springer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Justus Springer
-/
import Mathlib.Algebra.Category.GroupCat.FilteredColimits
import Mathlib.Algebra.Category.ModuleCat.Basic

#align_import algebra.category.Module.filtered_colimits from "leanprover-community/mathlib"@"806bbb0132ba63b93d5edbe4789ea226f8329979"

/-!
# The forgetful functor from `R`-modules preserves filtered colimits.

Forgetful functors from algebraic categories usually don't preserve colimits. However, they tend
to preserve _filtered_ colimits.

In this file, we start with a ring `R`, a small filtered category `J` and a functor
`F : J ‚•§ ModuleCat R`. We show that the colimit of `F ‚ãô forget‚ÇÇ (ModuleCat R) AddCommGroupCat`
(in `AddCommGroupCat`) carries the structure of an `R`-module, thereby showing that the forgetful
functor `forget‚ÇÇ (ModuleCat R) AddCommGroupCat` preserves filtered colimits. In particular, this
implies that `forget (ModuleCat R)` preserves filtered colimits.

-/


universe v u w

noncomputable section

open scoped Classical

open CategoryTheory CategoryTheory.Limits

open CategoryTheory.IsFiltered renaming max ‚Üí max' -- avoid name collision with `_root_.max`.

open AddMonCat.FilteredColimits (colimit_zero_eq colimit_add_mk_eq)

namespace ModuleCat.FilteredColimits

section

variable {R : Type u} [Ring R] {J : Type v} [Category.{w} J] [IsFiltered J]

variable (F : J ‚•§ ModuleCat.{w} R) [UnivLE.{v, w}]

/-- The colimit of `F ‚ãô forget‚ÇÇ (ModuleCat R) AddCommGroupCat` in the category `AddCommGroupCat`.
In the following, we will show that this has the structure of an `R`-module.
-/
abbrev M : AddCommGroupCat :=
  AddCommGroupCat.FilteredColimits.colimit
    (F ‚ãô forget‚ÇÇ (ModuleCat R) AddCommGroupCat)
set_option linter.uppercaseLean3 false in
#align Module.filtered_colimits.M ModuleCat.FilteredColimits.M

/-- The canonical projection into the colimit, as a quotient type. -/
abbrev M.mk : (Œ£ j, F.obj j) ‚Üí M F :=
  AddGroupCat.FilteredColimits.G.mk (F ‚ãô forget‚ÇÇ (ModuleCat R)
    AddCommGroupCat ‚ãô forget‚ÇÇ _ AddGroupCat)
set_option linter.uppercaseLean3 false in
#align Module.filtered_colimits.M.mk ModuleCat.FilteredColimits.M.mk

@[simp]
lemma M.equivShrink_symm_mk (x : Œ£ j, F.obj j) :
    (equivShrink _).symm (M.mk F x) = Quot.mk _ x := by
  apply Equiv.symm_apply_apply

lemma M.surjective_mk : Function.Surjective (M.mk F) := by
  intro x
  obtain ‚ü®x, rfl‚ü© := (equivShrink _).surjective x
  obtain ‚ü®x, rfl‚ü© := surjective_quot_mk _ x
  exact ‚ü®x, rfl‚ü©

theorem M.mk_eq (x y : Œ£ j, F.obj j)
    (h : ‚àÉ (k : J) (f : x.1 ‚ü∂ k) (g : y.1 ‚ü∂ k), F.map f x.2 = F.map g y.2) : M.mk F x = M.mk F y :=
  AddGroupCat.FilteredColimits.G.mk_eq _ _ _ h
set_option linter.uppercaseLean3 false in
#align Module.filtered_colimits.M.mk_eq ModuleCat.FilteredColimits.M.mk_eq

/-- The "unlifted" version of scalar multiplication in the colimit. -/
def colimitSMulAux (r : R) (x : Œ£ j, F.obj j) : M F :=
  M.mk F ‚ü®x.1, r ‚Ä¢ x.2‚ü©
set_option linter.uppercaseLean3 false in
#align Module.filtered_colimits.colimit_smul_aux ModuleCat.FilteredColimits.colimitSMulAux

theorem colimitSMulAux_eq_of_rel (r : R) (x y : Œ£ j, F.obj j)
    (h : Types.FilteredColimit.Rel (F ‚ãô forget (ModuleCat R)) x y) :
    colimitSMulAux F r x = colimitSMulAux F r y := by
  apply M.mk_eq
  obtain ‚ü®k, f, g, hfg‚ü© := h
  use k, f, g
  simp only [Functor.comp_obj, Functor.comp_map, forget_map] at hfg
  simp [hfg]
set_option linter.uppercaseLean3 false in
#align Module.filtered_colimits.colimit_smul_aux_eq_of_rel ModuleCat.FilteredColimits.colimitSMulAux_eq_of_rel

/-- Scalar multiplication in the colimit. See also `colimitSMulAux`. -/
instance colimitHasSmul : SMul R (M F) where
  smul r x := by
    refine Quot.lift (colimitSMulAux F r) ?_ ((equivShrink _).symm x)
    intro x y h
    apply colimitSMulAux_eq_of_rel
    apply Types.FilteredColimit.rel_of_quot_rel
    exact h

set_option linter.uppercaseLean3 false in
#align Module.filtered_colimits.colimit_has_smul ModuleCat.FilteredColimits.colimitHasSmul

@[simp]
theorem colimit_smul_mk_eq (r : R) (x : Œ£ j, F.obj j) : r ‚Ä¢ M.mk F x = M.mk F ‚ü®x.1, r ‚Ä¢ x.2‚ü© := by
  dsimp [HSMul.hSMul, colimitHasSmul]
  simp only [M.equivShrink_symm_mk]
  rfl
set_option linter.uppercaseLean3 false in
#align Module.filtered_colimits.colimit_smul_mk_eq ModuleCat.FilteredColimits.colimit_smul_mk_eq

-- Porting note: writing directly the `Module` instance makes things very slow.
instance colimitMulAction : MulAction R (M F) where
  one_smul x := by
    obtain ‚ü®‚ü®j, x‚ü©, rfl‚ü© := M.surjective_mk _ x
    rw [colimit_smul_mk_eq F 1 ‚ü®j, x‚ü©, one_smul]
  mul_smul r s x := by
    obtain ‚ü®‚ü®j, x‚ü©, rfl‚ü© := M.surjective_mk _ x
    rw [colimit_smul_mk_eq F (r * s) ‚ü®j, x‚ü©, colimit_smul_mk_eq F s ‚ü®j, x‚ü©,
      colimit_smul_mk_eq F r ‚ü®j, _‚ü©, mul_smul]

instance colimitSMulWithZero : SMulWithZero R (M F) :=
{ colimitMulAction F with
  smul_zero := fun r => by
    erw [colimit_zero_eq _ (IsFiltered.Nonempty.some : J), colimit_smul_mk_eq, smul_zero]
    rfl
  zero_smul := fun x => by
    obtain ‚ü®‚ü®j, x‚ü©, rfl‚ü© := M.surjective_mk _ x
    rw [colimit_smul_mk_eq, zero_smul, colimit_zero_eq _ j]
    rfl }

private theorem colimitModule.add_smul (r s : R) (x : (M F)) : (r + s) ‚Ä¢ x = r ‚Ä¢ x + s ‚Ä¢ x := by
  obtain ‚ü®‚ü®j, x‚ü©, rfl‚ü© := M.surjective_mk _ x
  erw [colimit_smul_mk_eq, _root_.add_smul, colimit_smul_mk_eq, colimit_smul_mk_eq,
      colimit_add_mk_eq _ ‚ü®j, _‚ü© ‚ü®j, _‚ü© j (ùüô j) (ùüô j), CategoryTheory.Functor.map_id, id_apply,
      id_apply]
  rfl

instance colimitModule : Module R (M F) :=
{ colimitMulAction F,
  colimitSMulWithZero F with
  smul_add := fun r x y => by
    obtain ‚ü®‚ü®i, x‚ü©, rfl‚ü© := M.surjective_mk _ x
    obtain ‚ü®‚ü®j, y‚ü©, rfl‚ü© := M.surjective_mk _ y
    erw [colimit_add_mk_eq _ ‚ü®i, _‚ü© ‚ü®j, _‚ü© (max' i j) (IsFiltered.leftToMax i j)
      (IsFiltered.rightToMax i j), colimit_smul_mk_eq, smul_add, colimit_smul_mk_eq]
    erw [colimit_smul_mk_eq, colimit_add_mk_eq _ ‚ü®i, _‚ü© ‚ü®j, _‚ü© (max' i j) (IsFiltered.leftToMax i j)
      (IsFiltered.rightToMax i j), LinearMap.map_smul, LinearMap.map_smul]
    rfl
  add_smul := colimitModule.add_smul F }
set_option linter.uppercaseLean3 false in
#align Module.filtered_colimits.colimit_module ModuleCat.FilteredColimits.colimitModule

/-- The bundled `R`-module giving the filtered colimit of a diagram. -/
def colimit : ModuleCat.{w} R :=
  ModuleCat.of R (M F)
set_option linter.uppercaseLean3 false in
#align Module.filtered_colimits.colimit ModuleCat.FilteredColimits.colimit

/-- The linear map from a given `R`-module in the diagram to the colimit module. -/
def coconeMorphism (j : J) : F.obj j ‚ü∂ colimit F :=
  { (AddCommGroupCat.FilteredColimits.colimitCocone
      (F ‚ãô forget‚ÇÇ (ModuleCat R) AddCommGroupCat)).Œπ.app j with
    map_smul' := fun r x => by erw [colimit_smul_mk_eq F r ‚ü®j, x‚ü©]; rfl }
set_option linter.uppercaseLean3 false in
#align Module.filtered_colimits.cocone_morphism ModuleCat.FilteredColimits.coconeMorphism

/-- The cocone over the proposed colimit module. -/
def colimitCocone : Cocone F where
  pt := colimit F
  Œπ :=
    { app := coconeMorphism F
      naturality := fun _ _' f =>
        LinearMap.coe_injective ((Types.colimitCocone (F ‚ãô forget (ModuleCat R))).Œπ.naturality f) }
set_option linter.uppercaseLean3 false in
#align Module.filtered_colimits.colimit_cocone ModuleCat.FilteredColimits.colimitCocone

/-- Given a cocone `t` of `F`, the induced monoid linear map from the colimit to the cocone point.
We already know that this is a morphism between additive groups. The only thing left to see is that
it is a linear map, i.e. preserves scalar multiplication.
-/
def colimitDesc (t : Cocone F) : colimit F ‚ü∂ t.pt where
  toFun :=
    (AddCommGroupCat.FilteredColimits.colimitCoconeIsColimit
      (F ‚ãô forget‚ÇÇ (ModuleCat R) AddCommGroupCat)).desc
        ((forget‚ÇÇ (ModuleCat R) AddCommGroupCat).mapCocone t)
  map_add' _ _ := map_add _ _ _
  map_smul' r x := by
    obtain ‚ü®‚ü®j, x‚ü©, rfl‚ü© := M.surjective_mk _ x
    erw [colimit_smul_mk_eq]
    simp only [Functor.mapCocone_pt, forget‚ÇÇ_obj, RingHom.id_apply]
    erw [AddCommGroupCat.FilteredColimits.colimitCoconeIsColimit_desc_apply,
      AddCommGroupCat.FilteredColimits.colimitCoconeIsColimit_desc_apply]
    exact LinearMap.map_smul (t.Œπ.app j) r x
set_option linter.uppercaseLean3 false in
#align Module.filtered_colimits.colimit_desc ModuleCat.FilteredColimits.colimitDesc

/-- The proposed colimit cocone is a colimit in `ModuleCat R`. -/
def colimitCoconeIsColimit : IsColimit (colimitCocone F) where
  desc := colimitDesc F
  fac t j :=
    LinearMap.coe_injective <|
      (Types.colimitCoconeIsColimit (F ‚ãô forget (ModuleCat R))).fac
        ((forget (ModuleCat R)).mapCocone t) j
  uniq t _ h :=
    LinearMap.coe_injective <|
      (Types.colimitCoconeIsColimit (F ‚ãô forget (ModuleCat R))).uniq
        ((forget (ModuleCat R)).mapCocone t) _ fun j => funext fun x => LinearMap.congr_fun (h j) x
set_option linter.uppercaseLean3 false in
#align Module.filtered_colimits.colimit_cocone_is_colimit ModuleCat.FilteredColimits.colimitCoconeIsColimit

instance forget‚ÇÇAddCommGroupPreservesFilteredColimits :
    PreservesFilteredColimits (forget‚ÇÇ (ModuleCat.{u} R) AddCommGroupCat.{u}) where
  preserves_filtered_colimits J _ _ :=
  { -- Porting note: without the curly braces for `F`
    -- here we get a confusing error message about universes.
    preservesColimit := fun {F : J ‚•§ ModuleCat.{u} R} =>
      preservesColimitOfPreservesColimitCocone (colimitCoconeIsColimit F)
        (AddCommGroupCat.FilteredColimits.colimitCoconeIsColimit
          (F ‚ãô forget‚ÇÇ (ModuleCat.{u} R) AddCommGroupCat.{u})) }
set_option linter.uppercaseLean3 false in
#align Module.filtered_colimits.forget‚ÇÇ_AddCommGroup_preserves_filtered_colimits ModuleCat.FilteredColimits.forget‚ÇÇAddCommGroupPreservesFilteredColimits

instance forgetPreservesFilteredColimits : PreservesFilteredColimits (forget (ModuleCat.{u} R)) :=
  Limits.compPreservesFilteredColimits (forget‚ÇÇ (ModuleCat R) AddCommGroupCat)
    (forget AddCommGroupCat)
set_option linter.uppercaseLean3 false in
#align Module.filtered_colimits.forget_preserves_filtered_colimits ModuleCat.FilteredColimits.forgetPreservesFilteredColimits

end

end ModuleCat.FilteredColimits
