/-
Copyright (c) 2021 Justus Springer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Justus Springer
-/
import Mathlib.Algebra.Category.Grp.FilteredColimits
import Mathlib.Algebra.Category.ModuleCat.Colimits

/-!
# The forgetful functor from `R`-modules preserves filtered colimits.

Forgetful functors from algebraic categories usually don't preserve colimits. However, they tend
to preserve _filtered_ colimits.

In this file, we start with a ring `R`, a small filtered category `J` and a functor
`F : J ‚•§ ModuleCat R`. We show that the colimit of `F ‚ãô forget‚ÇÇ (ModuleCat R) AddCommGrp`
(in `AddCommGrp`) carries the structure of an `R`-module, thereby showing that the forgetful
functor `forget‚ÇÇ (ModuleCat R) AddCommGrp` preserves filtered colimits. In particular, this
implies that `forget (ModuleCat R)` preserves filtered colimits.

-/


universe v u

noncomputable section

open CategoryTheory CategoryTheory.Limits

open CategoryTheory.IsFiltered renaming max ‚Üí max' -- avoid name collision with `_root_.max`.

open AddMonCat.FilteredColimits (colimit_zero_eq colimit_add_mk_eq)

namespace ModuleCat.FilteredColimits

section

variable {R : Type u} [Ring R] {J : Type v} [SmallCategory J] [IsFiltered J]
variable (F : J ‚•§ ModuleCat.{max v u, u} R)

/-- The colimit of `F ‚ãô forget‚ÇÇ (ModuleCat R) AddCommGrp` in the category `AddCommGrp`.
In the following, we will show that this has the structure of an `R`-module.
-/
abbrev M : AddCommGrp :=
  AddCommGrp.FilteredColimits.colimit.{v, u}
    (F ‚ãô forget‚ÇÇ (ModuleCat R) AddCommGrp.{max v u})

/-- The canonical projection into the colimit, as a quotient type. -/
abbrev M.mk : (Œ£ j, F.obj j) ‚Üí M F :=
  Quot.mk (Types.Quot.Rel (F ‚ãô forget (ModuleCat R)))

theorem M.mk_eq (x y : Œ£ j, F.obj j)
    (h : ‚àÉ (k : J) (f : x.1 ‚ü∂ k) (g : y.1 ‚ü∂ k), F.map f x.2 = F.map g y.2) : M.mk F x = M.mk F y :=
  Quot.eqvGen_sound (Types.FilteredColimit.eqvGen_quot_rel_of_rel (F ‚ãô forget (ModuleCat R)) x y h)

/-- The "unlifted" version of scalar multiplication in the colimit. -/
def colimitSMulAux (r : R) (x : Œ£ j, F.obj j) : M F :=
  M.mk F ‚ü®x.1, r ‚Ä¢ x.2‚ü©

theorem colimitSMulAux_eq_of_rel (r : R) (x y : Œ£ j, F.obj j)
    (h : Types.FilteredColimit.Rel (F ‚ãô forget (ModuleCat R)) x y) :
    colimitSMulAux F r x = colimitSMulAux F r y := by
  apply M.mk_eq
  obtain ‚ü®k, f, g, hfg‚ü© := h
  use k, f, g
  simp only [Functor.comp_obj, Functor.comp_map, forget_map] at hfg
  simp [hfg]

/-- Scalar multiplication in the colimit. See also `colimitSMulAux`. -/
instance colimitHasSMul : SMul R (M F) where
  smul r x := by
    refine Quot.lift (colimitSMulAux F r) ?_ x
    intro x y h
    apply colimitSMulAux_eq_of_rel
    apply Types.FilteredColimit.rel_of_quot_rel
    exact h

@[simp]
theorem colimit_smul_mk_eq (r : R) (x : Œ£ j, F.obj j) : r ‚Ä¢ M.mk F x = M.mk F ‚ü®x.1, r ‚Ä¢ x.2‚ü© :=
  rfl

private theorem colimitModule.one_smul (x : (M F)) : (1 : R) ‚Ä¢ x = x := by
  refine Quot.inductionOn x ?_; clear x; intro x; obtain ‚ü®j, x‚ü© := x
  erw [colimit_smul_mk_eq F 1 ‚ü®j, x‚ü©]
  simp
  rfl

-- Porting note (https://github.com/leanprover-community/mathlib4/pull/11083): writing directly the `Module` instance makes things very slow.
instance colimitMulAction : MulAction R (M F) where
  one_smul x := by
    refine Quot.inductionOn x ?_; clear x; intro x; obtain ‚ü®j, x‚ü© := x
    erw [colimit_smul_mk_eq F 1 ‚ü®j, x‚ü©, one_smul]
    rfl
  mul_smul r s x := by
    refine Quot.inductionOn x ?_; clear x; intro x; obtain ‚ü®j, x‚ü© := x
    erw [colimit_smul_mk_eq F (r * s) ‚ü®j, x‚ü©, colimit_smul_mk_eq F s ‚ü®j, x‚ü©,
      colimit_smul_mk_eq F r ‚ü®j, _‚ü©, mul_smul]

instance colimitSMulWithZero : SMulWithZero R (M F) :=
{ colimitMulAction F with
  smul_zero := fun r => by
    erw [colimit_zero_eq _ (IsFiltered.nonempty.some : J), colimit_smul_mk_eq, smul_zero]
    rfl
  zero_smul := fun x => by
    refine Quot.inductionOn x ?_; clear x; intro x; obtain ‚ü®j, x‚ü© := x
    erw [colimit_smul_mk_eq, zero_smul, colimit_zero_eq _ j]
    rfl }

private theorem colimitModule.add_smul (r s : R) (x : (M F)) : (r + s) ‚Ä¢ x = r ‚Ä¢ x + s ‚Ä¢ x := by
  refine Quot.inductionOn x ?_; clear x; intro x; obtain ‚ü®j, x‚ü© := x
  erw [colimit_smul_mk_eq, _root_.add_smul, colimit_smul_mk_eq, colimit_smul_mk_eq,
      colimit_add_mk_eq _ ‚ü®j, _‚ü© ‚ü®j, _‚ü© j (ùüô j) (ùüô j)]
  simp only [Functor.comp_obj, forget‚ÇÇ_obj, Functor.comp_map, CategoryTheory.Functor.map_id,
    forget‚ÇÇ_map]
  rfl

instance colimitModule : Module R (M F) :=
{ colimitMulAction F,
  colimitSMulWithZero F with
  smul_add := fun r x y => by
    refine Quot.induction_on‚ÇÇ x y ?_; clear x y; intro x y; obtain ‚ü®i, x‚ü© := x; obtain ‚ü®j, y‚ü© := y
    erw [colimit_add_mk_eq _ ‚ü®i, _‚ü© ‚ü®j, _‚ü© (max' i j) (IsFiltered.leftToMax i j)
      (IsFiltered.rightToMax i j), colimit_smul_mk_eq, smul_add, colimit_smul_mk_eq,
      colimit_smul_mk_eq, colimit_add_mk_eq _ ‚ü®i, _‚ü© ‚ü®j, _‚ü© (max' i j) (IsFiltered.leftToMax i j)
      (IsFiltered.rightToMax i j), LinearMap.map_smul, LinearMap.map_smul]
    rfl
  add_smul := colimitModule.add_smul F }

/-- The bundled `R`-module giving the filtered colimit of a diagram. -/
def colimit : ModuleCat.{max v u, u} R :=
  ModuleCat.of R (M F)

/-- The linear map from a given `R`-module in the diagram to the colimit module. -/
def coconeMorphism (j : J) : F.obj j ‚ü∂ colimit F :=
  ofHom
    { ((AddCommGrp.FilteredColimits.colimitCocone
      (F ‚ãô forget‚ÇÇ (ModuleCat R) AddCommGrp.{max v u})).Œπ.app j).hom with
    map_smul' := fun r x => by erw [colimit_smul_mk_eq F r ‚ü®j, x‚ü©]; rfl }

/-- The cocone over the proposed colimit module. -/
def colimitCocone : Cocone F where
  pt := colimit F
  Œπ :=
    { app := coconeMorphism F
      naturality := fun _ _' f =>
        hom_ext <| LinearMap.coe_injective
          ((Types.TypeMax.colimitCocone (F ‚ãô forget (ModuleCat R))).Œπ.naturality f) }

/-- Given a cocone `t` of `F`, the induced monoid linear map from the colimit to the cocone point.
We already know that this is a morphism between additive groups. The only thing left to see is that
it is a linear map, i.e. preserves scalar multiplication.
-/
def colimitDesc (t : Cocone F) : colimit F ‚ü∂ t.pt :=
  ofHom
    { ((AddCommGrp.FilteredColimits.colimitCoconeIsColimit
          (F ‚ãô forget‚ÇÇ (ModuleCat.{max v u} R) AddCommGrp.{max v u})).desc
      ((forget‚ÇÇ (ModuleCat R) AddCommGrp.{max v u}).mapCocone t)).hom with
    map_smul' := fun r x => by
      refine Quot.inductionOn x ?_; clear x; intro x; obtain ‚ü®j, x‚ü© := x
      erw [colimit_smul_mk_eq]
      exact LinearMap.map_smul (t.Œπ.app j).hom r x }

/-- The proposed colimit cocone is a colimit in `ModuleCat R`. -/
def colimitCoconeIsColimit : IsColimit (colimitCocone F) where
  desc := colimitDesc F
  fac t j :=
    hom_ext <| LinearMap.coe_injective <|
      (Types.TypeMax.colimitCoconeIsColimit.{v, u} (F ‚ãô forget (ModuleCat R))).fac
        ((forget (ModuleCat R)).mapCocone t) j
  uniq t _ h :=
    hom_ext <| LinearMap.coe_injective <|
      (Types.TypeMax.colimitCoconeIsColimit (F ‚ãô forget (ModuleCat R))).uniq
        ((forget (ModuleCat R)).mapCocone t) _ fun j => funext fun x => LinearMap.congr_fun
          (ModuleCat.hom_ext_iff.mp (h j)) x

instance forget‚ÇÇAddCommGroup_preservesFilteredColimits :
    PreservesFilteredColimits (forget‚ÇÇ (ModuleCat.{u} R) AddCommGrp.{u}) where
  preserves_filtered_colimits _ _ _ :=
  { preservesColimit := fun {F} =>
      preservesColimit_of_preserves_colimit_cocone (colimitCoconeIsColimit F)
        (AddCommGrp.FilteredColimits.colimitCoconeIsColimit
          (F ‚ãô forget‚ÇÇ (ModuleCat.{u} R) AddCommGrp.{u})) }

instance forget_preservesFilteredColimits : PreservesFilteredColimits (forget (ModuleCat.{u} R)) :=
  Limits.comp_preservesFilteredColimits (forget‚ÇÇ (ModuleCat R) AddCommGrp)
    (forget AddCommGrp)

instance forget_reflectsFilteredColimits : ReflectsFilteredColimits (forget (ModuleCat.{u} R)) where
  reflects_filtered_colimits _ := { reflectsColimit := reflectsColimit_of_reflectsIsomorphisms _ _ }

end

end ModuleCat.FilteredColimits
