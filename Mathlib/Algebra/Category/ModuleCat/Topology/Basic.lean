/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Richard Hill, Andrew Yang
-/
import Mathlib.Algebra.Category.ModuleCat.Colimits
import Mathlib.Algebra.Category.ModuleCat.Limits
import Mathlib.Topology.Algebra.Module.ModuleTopology
import Mathlib.Topology.Category.TopCat.Limits.Basic

/-!
# The category `TopModuleCat R` of topological modules

We define `TopModuleCat R`, the category of topological modules, and show that
it has all limits and colimits.

We also provide various adjunctions:
- `TopModuleCat.withModuleTopologyAdj`:
  equipping the module topology is left adjoint to the forgetful functor into `ModuleCat R`.
- `TopModuleCat.indiscreteAdj`:
  equipping the indiscrete topology is right adjoint to the forgetful functor into `ModuleCat R`.
- `TopModuleCat.freeAdj`:
  the free-forgetful adjunction between `TopModuleCat R` and `TopCat`.

## Future projects
Show that the forgetful functor to `TopCat` preserves filtered colimits.
-/

universe v u

variable (R : Type u) [Ring R] [TopologicalSpace R]

open CategoryTheory ConcreteCategory

/-- The category of topological modules. -/
structure TopModuleCat extends ModuleCat.{v} R where
  /-- The underlying topological space. -/
  [topologicalSpace : TopologicalSpace carrier]
  [isTopologicalAddGroup : IsTopologicalAddGroup carrier]
  [continuousSMul : ContinuousSMul R carrier]

namespace TopModuleCat

noncomputable instance : CoeSort (TopModuleCat.{v} R) (Type v) := ‚ü®fun M ‚Ü¶ M.toModuleCat‚ü©

attribute [instance] topologicalSpace isTopologicalAddGroup continuousSMul

/-- Make an object in `TopModuleCat R` from an unbundled topological module. -/
abbrev of (M : Type v) [AddCommGroup M] [Module R M] [TopologicalSpace M] [ContinuousAdd M]
    [ContinuousSMul R M] : TopModuleCat R :=
  have : ContinuousNeg M := ‚ü®by convert continuous_const_smul (-1 : R) (T := M); ext; simp‚ü©
  have : IsTopologicalAddGroup M := ‚ü®‚ü©
  ‚ü®.of R M‚ü©

lemma coe_of (M : Type v) [AddCommGroup M] [Module R M] [TopologicalSpace M] [ContinuousAdd M]
    [ContinuousSMul R M] : (of R M) = M := rfl

variable {R} in
/-- Homs in `TopModuleCat` as one field structures over `ContinuousLinearMap`. -/
structure Hom (X Y : TopModuleCat.{v} R) where
  -- use `ofHom` instead
  private ofHom' ::
  /-- The underlying continuous linear map. Use `hom` instead. -/
  hom' : X ‚ÜíL[R] Y

instance : Category (TopModuleCat R) where
  Hom := Hom
  id M := ‚ü®ContinuousLinearMap.id R M‚ü©
  comp œÜ œà := ‚ü®œà.hom' ‚àòL œÜ.hom'‚ü©

instance : ConcreteCategory (TopModuleCat R) (¬∑ ‚ÜíL[R] ¬∑) where
  hom   := Hom.hom'
  ofHom := Hom.ofHom'

variable {R} in
/-- Cast a hom in `TopModuleCat` into a continuous linear map. -/
abbrev Hom.hom {X Y : TopModuleCat R} (f : X.Hom Y) : X ‚ÜíL[R] Y :=
  ConcreteCategory.hom (C := TopModuleCat R) f

variable {R} in
/-- Construct a hom in `TopModuleCat` from a continuous linear map. -/
abbrev ofHom {X Y : Type v}
    [AddCommGroup X] [Module R X] [TopologicalSpace X] [ContinuousAdd X] [ContinuousSMul R X]
    [AddCommGroup Y] [Module R Y] [TopologicalSpace Y] [ContinuousAdd Y] [ContinuousSMul R Y]
    (f : X ‚ÜíL[R] Y) : of R X ‚ü∂ of R Y :=
  ConcreteCategory.ofHom f

@[simp] lemma hom_ofHom {X Y : Type v}
    [AddCommGroup X] [Module R X] [TopologicalSpace X] [ContinuousAdd X] [ContinuousSMul R X]
    [AddCommGroup Y] [Module R Y] [TopologicalSpace Y] [ContinuousAdd Y] [ContinuousSMul R Y]
    (f : X ‚ÜíL[R] Y) :
    (ofHom f).hom = f := rfl

@[simp] lemma ofHom_hom {X Y : TopModuleCat R} (f : X.Hom Y) : ofHom f.hom = f := rfl

@[simp] lemma hom_comp {X Y Z : TopModuleCat R} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).hom = g.hom.comp f.hom := rfl

@[simp] lemma hom_id (X : TopModuleCat R) : hom (ùüô X) = .id _ _ := rfl

/-- Use the `ConcreteCategory.hom` projection for `@[simps]` lemmas. -/
def Hom.Simps.hom (A B : TopModuleCat.{v} R) (f : A.Hom B) :=
  f.hom

initialize_simps_projections Hom (hom' ‚Üí hom)

variable {R} in
/-- Construct an iso in `TopModuleCat` from a continuous linear equiv. -/
def ofIso {X Y : TopModuleCat R} (e : X ‚âÉL[R] Y) : X ‚âÖ Y :=
  ‚ü®ofHom e.toContinuousLinearMap, ofHom e.symm.toContinuousLinearMap,
    by ext; exact e.symm_apply_apply _, by ext; exact e.apply_symm_apply _‚ü©

variable {R} in
/-- Cast an iso in `TopModuleCat` into a continuous linear equiv. -/
def _root_.CategoryTheory.Iso.toContinuousLinearEquiv
    {X Y : TopModuleCat R} (e : X ‚âÖ Y) : X ‚âÉL[R] Y where
  __ := e.hom.hom
  invFun := e.inv.hom
  left_inv x := by cat_disch
  right_inv x := by cat_disch

instance {X Y : TopModuleCat R} : AddCommGroup (X ‚ü∂ Y) where
  add f g := ofHom (f.hom + g.hom)
  zero := ofHom 0
  __ := Equiv.addCommGroup CategoryTheory.ConcreteCategory.homEquiv

instance : Preadditive (TopModuleCat R) where
  add_comp _ _ _ _ _ _  := ConcreteCategory.ext (ContinuousLinearMap.comp_add _ _ _)
  comp_add _ _ _ _ _ _  := ConcreteCategory.ext (ContinuousLinearMap.add_comp _ _ _)

section

variable {M‚ÇÅ M‚ÇÇ : TopModuleCat R}

@[simp] lemma hom_zero : (0 : M‚ÇÅ ‚ü∂ M‚ÇÇ).hom = 0 := rfl
@[simp] lemma hom_zero_apply (m : M‚ÇÅ) : (0 : M‚ÇÅ ‚ü∂ M‚ÇÇ).hom m = 0 := rfl
@[simp] lemma hom_add (œÜ‚ÇÅ œÜ‚ÇÇ : M‚ÇÅ ‚ü∂ M‚ÇÇ) : (œÜ‚ÇÅ + œÜ‚ÇÇ).hom = œÜ‚ÇÅ.hom + œÜ‚ÇÇ.hom := rfl
@[simp] lemma hom_neg (œÜ : M‚ÇÅ ‚ü∂ M‚ÇÇ) : (- œÜ).hom = - œÜ.hom := rfl
@[simp] lemma hom_sub (œÜ‚ÇÅ œÜ‚ÇÇ : M‚ÇÅ ‚ü∂ M‚ÇÇ) : (œÜ‚ÇÅ - œÜ‚ÇÇ).hom = œÜ‚ÇÅ.hom - œÜ‚ÇÇ.hom := rfl
@[simp] lemma hom_nsmul (n : ‚Ñï) (œÜ : M‚ÇÅ ‚ü∂ M‚ÇÇ) : (n ‚Ä¢ œÜ).hom = n ‚Ä¢ œÜ.hom := rfl
@[simp] lemma hom_zsmul (n : ‚Ñ§) (œÜ : M‚ÇÅ ‚ü∂ M‚ÇÇ) : (n ‚Ä¢ œÜ).hom = n ‚Ä¢ œÜ.hom := rfl

end

section CommRing

variable {S : Type*} [CommRing S] [TopologicalSpace S]

instance {X Y : TopModuleCat S} : Module S (X ‚ü∂ Y) where
  smul r f := ofHom (r ‚Ä¢ f.hom)
  __ := Equiv.module _ CategoryTheory.ConcreteCategory.homEquiv

instance [CommRing S] : Linear S (TopModuleCat S) where
  smul_comp _ _ _ _ _ _ := ConcreteCategory.ext (ContinuousLinearMap.comp_smul _ _ _)
  comp_smul _ _ _ _ _ _ := ConcreteCategory.ext (ContinuousLinearMap.smul_comp _ _ _)

@[simp]
lemma hom_smul {M‚ÇÅ M‚ÇÇ : TopModuleCat S} (s : S) (œÜ : M‚ÇÅ ‚ü∂ M‚ÇÇ) : (s ‚Ä¢ œÜ).hom = s ‚Ä¢ œÜ.hom := rfl

end CommRing

instance (M : TopModuleCat R) : TopologicalSpace M := M.2
instance (M : TopModuleCat R) : IsTopologicalAddGroup M := M.3

instance : HasForget‚ÇÇ (TopModuleCat R) (ModuleCat R) where
  forget‚ÇÇ :=
  { obj M := ModuleCat.of R M
    map œÜ := ModuleCat.ofHom œÜ.hom }

instance : HasForget‚ÇÇ (TopModuleCat R) TopCat where
  forget‚ÇÇ :=
  { obj M := .of M
    map œÜ   := TopCat.ofHom ‚ü®œÜ, œÜ.1.2‚ü© }

instance : (forget‚ÇÇ (TopModuleCat R) TopCat).ReflectsIsomorphisms where
  reflects {X Y} f hf := by
    let e : X ‚âÉL[R] Y :=
      { __ := f.hom, __ := TopCat.homeoOfIso (asIso ((forget‚ÇÇ (TopModuleCat R) TopCat).map f)) }
    change IsIso (ofIso e).hom
    infer_instance

@[simp]
lemma hom_forget‚ÇÇ_TopCat_map {X Y : TopModuleCat R} (f : X ‚ü∂ Y) :
    ((forget‚ÇÇ _ TopCat).map f).hom = f.hom := rfl

@[simp]
lemma forget‚ÇÇ_TopCat_obj {X : TopModuleCat R} : ((forget‚ÇÇ _ TopCat).obj X : Type _) = X := rfl

open Limits

section Colimit

variable {R}

variable {M : ModuleCat R} {I : Type*} {X : I ‚Üí TopModuleCat R} (f : ‚àÄ i, (X i).toModuleCat ‚ü∂ M)

/-- The coinduced topology on `M` from a family of continuous linear maps into `M`, which is the
finest topology that makes it into a topological module and makes every map continuous. -/
def coinduced : TopModuleCat R :=
  letI : TopologicalSpace M := sInf { t | @ContinuousSMul R M _ _ t ‚àß @ContinuousAdd M t _ ‚àß
      ‚àÄ i, (X i).topologicalSpace.coinduced (f i) ‚â§ t }
  have : ContinuousAdd M := continuousAdd_sInf fun _ hs ‚Ü¶ hs.2.1
  have : ContinuousSMul R M := continuousSMul_sInf fun _ hs ‚Ü¶ hs.1
  .of R M

/-- The maps into the coinduced topology as homs in `TopModuleCat R`. -/
def toCoinduced (i) : X i ‚ü∂ coinduced f :=
  ofHom (Y := coinduced f)
    ‚ü®(f i).hom, continuous_iff_coinduced_le.mpr (le_sInf fun _ hœÑ ‚Ü¶ hœÑ.2.2 i)‚ü©

/-- The cocone of topological modules associated to a cocone over the underlying modules, where
the cocone point is given the coinduced topology. This is colimiting when the given cocone is. -/
def ofCocone {J : Type*} [Category J] {F : J ‚•§ TopModuleCat R}
    (c : Cocone (F ‚ãô forget‚ÇÇ _ (ModuleCat R))) : Cocone F where
  pt := coinduced c.Œπ.app
  Œπ :=
  { app := toCoinduced c.Œπ.app,
    naturality {X Y} f := by ext x; exact congr($(c.Œπ.naturality f).hom x) }

/-- Given a colimit cocone over the underlying modules, equipping the cocone point with
the coinduced topology gives a colimit cocone in `TopModuleCat R`. -/
def isColimit {J : Type*} [Category J] {F : J ‚•§ TopModuleCat R}
    {c : Cocone (F ‚ãô forget‚ÇÇ _ (ModuleCat R))} (hc : IsColimit c) :
    IsColimit (ofCocone c) where
  desc s := ofHom (X := (ofCocone c).pt) ‚ü®(hc.desc ((forget‚ÇÇ _ _).mapCocone s)).hom, by
    rw [continuous_iff_le_induced]
    refine sInf_le ‚ü®continuousSMul_induced (M‚ÇÇ := s.pt) (hc.desc ((forget‚ÇÇ _ _).mapCocone s)).hom,
      continuousAdd_induced (N := s.pt) (hc.desc ((forget‚ÇÇ _ _).mapCocone s)).hom, fun i ‚Ü¶ ?_‚ü©
    rw [coinduced_le_iff_le_induced, induced_compose, ‚Üê continuous_iff_le_induced]
    change Continuous (X := F.obj i) (Y := s.pt)
      (c.Œπ.app i ‚â´ hc.desc ((forget‚ÇÇ _ (ModuleCat R)).mapCocone s)).hom
    rw [hc.fac]
    exact (s.Œπ.app i).hom.2‚ü©
  fac s i := by ext x; exact congr($(hc.fac ((forget‚ÇÇ _ _).mapCocone s) i).hom x)
  uniq s m H := by
    ext x
    refine congr($(hc.uniq ((forget‚ÇÇ _ _).mapCocone s) ((forget‚ÇÇ _ _).map m) fun j ‚Ü¶ ?_).hom x)
    ext y
    exact congr($(H j).hom y)

instance {J : Type*} [Category J] {F : J ‚•§ TopModuleCat R}
    [HasColimit (F ‚ãô forget‚ÇÇ _ (ModuleCat R))] : HasColimit F :=
  ‚ü®_, isColimit (colimit.isColimit _)‚ü©

instance {J : Type*} [Category J] [HasColimitsOfShape J (ModuleCat.{v} R)] :
    HasColimitsOfShape J (TopModuleCat.{v} R) where

instance : HasColimits (TopModuleCat.{v} R) where

end Colimit

section Limit

variable {R}

variable {M : ModuleCat R} {I : Type*} {X : I ‚Üí TopModuleCat R} (f : ‚àÄ i, M ‚ü∂ (X i).toModuleCat)

/-- The induced topology on `M` from a family of continuous linear maps from `M`, which is the
coarsest topology that makes every map continuous. -/
def induced : TopModuleCat R :=
  letI : TopologicalSpace M := ‚®Ö i, (X i).topologicalSpace.induced (f i)
  have : ContinuousAdd M := continuousAdd_iInf fun _ ‚Ü¶ continuousAdd_induced _
  have : ContinuousSMul R M := continuousSMul_iInf fun _ ‚Ü¶ continuousSMul_induced _
  .of R M

/-- The maps from the induced topology as homs in `TopModuleCat R`. -/
def fromInduced (i) : induced f ‚ü∂ X i :=
  ofHom (X := induced f) ‚ü®(f i).hom, continuous_iff_le_induced.mpr (iInf_le _ i)‚ü©

open Limits

/-- The cone of topological modules associated to a cone over the underlying modules, where
the cone point is given the induced topology. This is limiting when the given cone is. -/
def ofCone {J : Type*} [Category J] {F : J ‚•§ TopModuleCat R}
    (c : Cone (F ‚ãô forget‚ÇÇ _ (ModuleCat R))) : Cone F where
  pt := induced c.œÄ.app
  œÄ :=
  { app := fromInduced c.œÄ.app,
    naturality {X Y} f := by ext x; exact congr($(c.œÄ.naturality f).hom x) }

/-- Given a limit cone over the underlying modules, equipping the cone point with
the induced topology gives a limit cone in `TopModuleCat R`. -/
def isLimit {J : Type*} [Category J] {F : J ‚•§ TopModuleCat R}
    {c : Cone (F ‚ãô forget‚ÇÇ _ (ModuleCat R))} (hc : IsLimit c) :
    IsLimit (ofCone c) where
  lift s := ofHom (Y := (ofCone c).pt) ‚ü®(hc.lift ((forget‚ÇÇ _ _).mapCone s)).hom, by
    rw [continuous_iff_coinduced_le]
    refine le_iInf fun i ‚Ü¶ ?_
    rw [coinduced_le_iff_le_induced, induced_compose, ‚Üê continuous_iff_le_induced]
    change Continuous (X := s.pt) (Y := F.obj i)
      (hc.lift ((forget‚ÇÇ _ (ModuleCat R)).mapCone s) ‚â´ c.œÄ.app i).hom
    rw [hc.fac]
    exact (s.œÄ.app i).hom.2‚ü©
  fac s i := by ext x; exact congr($(hc.fac ((forget‚ÇÇ _ _).mapCone s) i).hom x)
  uniq s m H := by
    ext x
    refine congr($(hc.uniq ((forget‚ÇÇ _ _).mapCone s) ((forget‚ÇÇ _ _).map m) fun j ‚Ü¶ ?_).hom x)
    ext y
    exact congr($(H j).hom y)

instance hasLimit_of_hasLimit_forget‚ÇÇ {J : Type*} [Category J] {F : J ‚•§ TopModuleCat.{v} R}
    [HasLimit (F ‚ãô forget‚ÇÇ _ (ModuleCat.{v} R))] : HasLimit F :=
  ‚ü®_, isLimit (limit.isLimit _)‚ü©

instance {J : Type*} [Category J] [HasLimitsOfShape J (ModuleCat.{v} R)] :
    HasLimitsOfShape J (TopModuleCat.{v} R) where
  has_limit _ := hasLimit_of_hasLimit_forget‚ÇÇ

instance : HasLimits (TopModuleCat.{v} R) where
  has_limits_of_shape _ _ := ‚ü®fun _ ‚Ü¶ hasLimit_of_hasLimit_forget‚ÇÇ‚ü©

instance {J : Type*} [Category J] {F : J ‚•§ TopModuleCat.{v} R}
    [HasLimit (F ‚ãô forget‚ÇÇ _ (ModuleCat.{v} R))]
    [PreservesLimit (F ‚ãô forget‚ÇÇ _ (ModuleCat.{v} R)) (forget _)] :
    PreservesLimit F (forget‚ÇÇ _ TopCat) :=
  preservesLimit_of_preserves_limit_cone (isLimit (limit.isLimit _))
    (TopCat.isLimitConeOfForget (F := F ‚ãô forget‚ÇÇ _ TopCat)
      ((forget _).mapCone (getLimitCone (F ‚ãô forget‚ÇÇ _ (ModuleCat.{v} R))).1:)
      (isLimitOfPreserves (forget (ModuleCat R)) (limit.isLimit _)))

instance {J : Type*} [Category J]
    [HasLimitsOfShape J (ModuleCat.{v} R)]
    [PreservesLimitsOfShape J (forget (ModuleCat.{v} R))] :
    PreservesLimitsOfShape J (forget‚ÇÇ (TopModuleCat.{v} R) TopCat) where

instance : PreservesLimits (forget‚ÇÇ (TopModuleCat.{v} R) TopCat.{v}) where

end Limit

section Adjunction

/-- The functor equipping a module over a topological ring with the finest possible
topology making it into a topological module. This is left adjoint to the forgetful functor. -/
def withModuleTopology : ModuleCat R ‚•§ TopModuleCat R where
  obj X :=
    letI := moduleTopology R X
    letI := IsModuleTopology.topologicalAddGroup R X
    .of R X
  map {X Y} f :=
    letI := moduleTopology R X
    letI := moduleTopology R Y
    letI := IsModuleTopology.topologicalAddGroup R Y
    ‚ü®f.hom, IsModuleTopology.continuous_of_linearMap f.hom‚ü©

/-- The adjunction between `withModuleTopology` and the forgetful functor. -/
def withModuleTopologyAdj : withModuleTopology R ‚ä£ forget‚ÇÇ (TopModuleCat R) (ModuleCat R) where
  unit := ùüô _
  counit :=
  { app X := ofHom (X := (withModuleTopology R).obj (.of R X))
      ‚ü®.id, IsModuleTopology.continuous_of_linearMap _‚ü© }

instance : (forget‚ÇÇ (TopModuleCat R) (ModuleCat R)).IsRightAdjoint := ‚ü®_, ‚ü®withModuleTopologyAdj R‚ü©‚ü©
instance : (withModuleTopology R).IsLeftAdjoint := ‚ü®_, ‚ü®withModuleTopologyAdj R‚ü©‚ü©

/-- The functor equipping a module with the indiscrete topology.
This is right adjoint to the forgetful functor. -/
def indiscrete : ModuleCat.{v} R ‚•§ TopModuleCat.{v} R where
  obj X :=
    letI : TopologicalSpace X := ‚ä§
    haveI : ContinuousAdd X := ‚ü®by rw [continuous_iff_coinduced_le]; exact le_top‚ü©
    haveI : ContinuousSMul R X := ‚ü®by rw [continuous_iff_coinduced_le]; exact le_top‚ü©
    .of R X
  map {X Y} f :=
    letI : TopologicalSpace X := ‚ä§
    letI : TopologicalSpace Y := ‚ä§
    ConcreteCategory.ofHom (C := TopModuleCat R)
      ‚ü®f.hom, by rw [continuous_iff_coinduced_le]; exact le_top‚ü©

/-- The adjunction between the forgetful functor and the indiscrete topology functor. -/
def indiscreteAdj : forget‚ÇÇ (TopModuleCat.{v} R) (ModuleCat.{v} R) ‚ä£ indiscrete.{v} R where
  counit := ùüô _
  unit := { app X := ConcreteCategory.ofHom (C := TopModuleCat R)
              ‚ü®.id, by rw [continuous_iff_coinduced_le]; exact le_top‚ü© }

instance : (forget‚ÇÇ (TopModuleCat.{v} R) (ModuleCat.{v} R)).IsLeftAdjoint := ‚ü®_, ‚ü®indiscreteAdj R‚ü©‚ü©
instance : (indiscrete.{v} R).IsRightAdjoint := ‚ü®_, ‚ü®indiscreteAdj R‚ü©‚ü©

/-- The free topological module over a topological space. -/
noncomputable
def freeObj (X : TopCat.{v}) : TopModuleCat.{max v u} R :=
  letI : TopologicalSpace (X ‚Üí‚ÇÄ R) := sInf
    { t | @ContinuousSMul R _ _ _ t ‚àß @ContinuousAdd _ t _ ‚àß
      X.str.coinduced (Finsupp.single ¬∑ 1) ‚â§ t }
  letI : ContinuousAdd (X ‚Üí‚ÇÄ R) := continuousAdd_sInf fun _ h ‚Ü¶ h.2.1
  letI : ContinuousSMul R (X ‚Üí‚ÇÄ R) := continuousSMul_sInf fun _ h ‚Ü¶ h.1
  of R (X ‚Üí‚ÇÄ R)

lemma coe_freeObj (X : TopCat.{v}) : freeObj R X = (X ‚Üí‚ÇÄ R) := rfl

/-- The free topological module over a topological space is functorial. -/
noncomputable
def freeMap {X Y : TopCat.{v}} (f : X ‚ü∂ Y) : freeObj R X ‚ü∂ freeObj R Y :=
  ConcreteCategory.ofHom ‚ü®Finsupp.lmapDomain _ _ f.hom, by
    rw [continuous_iff_coinduced_le]
    refine le_sInf fun (œÑ : TopologicalSpace (_ ‚Üí‚ÇÄ R)) ‚ü®hœÑ‚ÇÅ, hœÑ‚ÇÇ, hœÑ‚ÇÉ‚ü© ‚Ü¶ ?_
    rw [coinduced_le_iff_le_induced]
    refine sInf_le ‚ü®continuousSMul_induced (Finsupp.lmapDomain _ _ f.hom),
      continuousAdd_induced (Finsupp.lmapDomain _ _ f.hom), ?_‚ü©
    rw [‚Üê coinduced_le_iff_le_induced]
    grw [‚Üê hœÑ‚ÇÉ, ‚Üê coinduced_mono (continuous_iff_coinduced_le.mp f.hom.2)]
    rw [coinduced_compose, coinduced_compose]
    congr! 1
    ext x
    simp [coe_freeObj]‚ü©

lemma freeMap_map {X Y : TopCat} (f : X ‚ü∂ Y) (v : X ‚Üí‚ÇÄ R) :
    (freeMap R f : (X ‚Üí‚ÇÄ R) ‚Üí (Y ‚Üí‚ÇÄ R)) v = Finsupp.mapDomain f.hom v := rfl

/-- The free topological module over a topological space as a functor.
This is left adjoint to the forgetful functor. -/
@[simps] noncomputable
def free : TopCat.{v} ‚•§ TopModuleCat.{max v u} R :=
  { obj := freeObj R
    map f := freeMap R f
    map_id M := by ext x; exact DFunLike.congr_fun (Finsupp.lmapDomain_id _ _) x
    map_comp f g := by ext; exact DFunLike.congr_fun (Finsupp.lmapDomain_comp _ _ f.hom g.hom) _ }

/-- The free-forgetful adjoint for `TopModuleCat R`. -/
noncomputable
def freeAdj : free.{max v u} R ‚ä£ forget‚ÇÇ (TopModuleCat.{max v u} R) TopCat.{max v u} where
  unit :=
  { app X := TopCat.ofHom ‚ü®(Finsupp.single ¬∑ 1),
      continuous_iff_coinduced_le.mpr (le_sInf fun _ h ‚Ü¶ h.2.2)‚ü©,
    naturality {X Y} f := by ext x; simp [freeMap_map] }
  counit :=
  { app X := ConcreteCategory.ofHom (C := TopModuleCat R) ‚ü®Finsupp.lift _ R X id, by
      rw [continuous_iff_le_induced]
      refine sInf_le ‚ü®continuousSMul_induced (Finsupp.lift _ R X id),
        continuousAdd_induced (Finsupp.lift _ R X id), ?_‚ü©
      rw [coinduced_le_iff_le_induced, induced_compose]
      convert induced_id.symm.le
      ext
      simp [coe_freeObj]‚ü©,
    naturality {X Y} f := by
      ext1
      apply ContinuousLinearMap.coe_injective
      refine Finsupp.lhom_ext' fun a ‚Ü¶ LinearMap.ext_ring ?_
      dsimp [freeObj, freeMap]
      simp }
  left_triangle_components X := by
    ext1
    apply ContinuousLinearMap.coe_injective
    refine Finsupp.lhom_ext' fun a ‚Ü¶ LinearMap.ext_ring ?_
    simp [freeMap, freeObj]
  right_triangle_components X := by
    ext
    simp [freeObj]

instance : (forget‚ÇÇ (TopModuleCat.{max v u} R) TopCat).IsRightAdjoint := ‚ü®_, ‚ü®freeAdj R‚ü©‚ü©
instance : (free.{max v u} R).IsLeftAdjoint := ‚ü®_, ‚ü®freeAdj R‚ü©‚ü©

end Adjunction

end TopModuleCat
