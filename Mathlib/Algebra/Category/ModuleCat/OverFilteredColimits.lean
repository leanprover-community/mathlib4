/-
Copyright (c) 2024 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import Mathlib.Algebra.Ring.Hom.Defs
import Mathlib.Algebra.Module.Defs
import Mathlib.CategoryTheory.Limits.ConcreteCategory
import Mathlib.CategoryTheory.ConcreteCategory.Elementwise

/-!
# Module structures of filtered colimits of abelian groups over filtered colimts of rings

Let `R` be the filtered colimit of rings `{Râ±¼}` and `M` be the filtered colimit of abelian groups
`{Mâ±¼}`  with `j` running through some filtered index category `J`. If for each `j âˆˆ J`, `Mâ±¼` is
an `Râ±¼`-module such that the `Râ±¼`-actions are compatible, then `M` is an `Râ±¼`-module for all `j`
and `M` is an `R`-module.

## Implementation notes

We choose not to use `PresheafOfModules` to avoid code duplication:
consider `R : J â¥¤ CommRingCat` and `M : J â¥¤ AddCommGrp`, then `colimit M` is both a
`colimit R`-module and a `colimit (R â‹™ forgetâ‚‚ CommRingCat RingCat)`-module; the two module
structures are virtually the same. This situation manifests in stalks of sheaves of modules:
for any ringed space `X` and a sheaf of `ğ’ª_X`-module `â„³`, we want to think the stalk `â„³â‚“` as an
`ğ’ª_{X,x}`-module. But since `PresheafOfModules` requires a presheaf of `RingCat` not `CommRingCat`,
we need to compose the sheaf with forgetful functors, but we don't want to think about the
difference between `ğ’ª_{X, x}` as a colimit in `CommRing` and `ğ’ª_{X, x}` as a colimit in `RingCat`
all the time. So we ask `R` and `M` to be functors into concrete categories which behaves like rings
and abelian groups respectively.

-/

open CategoryTheory Category Limits Opposite

universe u u' v v' w

section

variable {J : Type w} [Category J] [IsFiltered J]
variable {â„œğ”¦ğ”«ğ”¤ : Type u} [Category.{u'} â„œğ”¦ğ”«ğ”¤] [ConcreteCategory.{w} â„œğ”¦ğ”«ğ”¤]
variable {ğ”„ğ”Ÿ : Type v} [Category.{v'} ğ”„ğ”Ÿ] [ConcreteCategory.{w} ğ”„ğ”Ÿ]

attribute [local instance] ConcreteCategory.hasCoeToSort
attribute [local instance] ConcreteCategory.instFunLike

variable [âˆ€ x : â„œğ”¦ğ”«ğ”¤, Semiring x] [âˆ€ x : ğ”„ğ”Ÿ, AddCommMonoid x]
variable [âˆ€ x y : â„œğ”¦ğ”«ğ”¤, RingHomClass (x âŸ¶ y) x y]
variable [âˆ€ x y : ğ”„ğ”Ÿ, AddMonoidHomClass (x âŸ¶ y) x y]

variable (â„› : J â¥¤ â„œğ”¦ğ”«ğ”¤) (â„³ : J â¥¤ ğ”„ğ”Ÿ)
variable [HasColimit â„›] [HasColimit â„³]
variable [PreservesColimit â„› (forget â„œğ”¦ğ”«ğ”¤)] [PreservesColimit â„³ (forget ğ”„ğ”Ÿ)]

variable [âˆ€ c, Module (â„›.obj c) (â„³.obj c)]
variable [compatible_smul : Fact $ âˆ€ {câ‚ câ‚‚ : J} (iâ‚ : câ‚ âŸ¶ câ‚‚) (r : â„›.obj câ‚) (m : â„³.obj câ‚),
    â„³.map iâ‚ (r â€¢ m) = â„›.map iâ‚ r â€¢ â„³.map iâ‚ m]

namespace Module.overFilteredColimits

variable {â„› â„³} in
/--
Let `R` be the filtered colimit of rings `{Râ±¼}` and `M` be the filtered colimit of
abelian groups `{Mâ±¼}`  with the same indexing set `j âˆˆ J`, if for each `j âˆˆ J`, `Mâ±¼` is an `Râ±¼` such
that the `Râ±¼`-action is compatible, then there is a heterogeneous scalar multiplication
`Ráµ¢ â†’ Mâ±¼ â†’ Mâ‚–` for every `i â†’ j` and `i â†’ k`.
-/
def hSMul {câ‚ câ‚‚ câ‚ƒ : J} (iâ‚ : câ‚ âŸ¶ câ‚ƒ) (iâ‚‚ : câ‚‚ âŸ¶ câ‚ƒ)
    (r : â„›.obj câ‚) (m : â„³.obj câ‚‚) : â„³.obj câ‚ƒ :=
  (â„›.map iâ‚ r) â€¢ (â„³.map iâ‚‚ m)

section hSMul

variable {câ‚ câ‚‚ câ‚ƒ : J} (iâ‚ : câ‚ âŸ¶ câ‚ƒ) (iâ‚‚ : câ‚‚ âŸ¶ câ‚ƒ)
variable (r : â„›.obj câ‚) (m : â„³.obj câ‚‚)

@[simp]
lemma one_hSMul :
    hSMul iâ‚ iâ‚‚ (1 : â„›.obj câ‚) m = (â„³.map iâ‚‚ m) := by
  simp [hSMul]

lemma mul_hSMul (râ‚ râ‚‚ : â„›.obj câ‚) : hSMul iâ‚ iâ‚‚ (râ‚ * râ‚‚) m =
    hSMul iâ‚ (ğŸ™ _) râ‚ (hSMul iâ‚ iâ‚‚ râ‚‚ m) := by
  simp only [hSMul, map_mul, mul_smul]
  rw [â„³.map_id, id_apply]

@[simp]
lemma hSMul_zero : hSMul (â„³ := â„³) iâ‚ iâ‚‚ r 0 = 0 := by
  simp [hSMul]

lemma hSMul_add (mâ‚ mâ‚‚ : â„³.obj câ‚‚) : hSMul iâ‚ iâ‚‚ r (mâ‚ + mâ‚‚) =
    hSMul iâ‚ iâ‚‚ r mâ‚ + hSMul iâ‚ iâ‚‚ r mâ‚‚ := by
  simp [hSMul, smul_add]

lemma add_hSMul (râ‚ râ‚‚ : â„›.obj câ‚) (m : â„³.obj câ‚‚) :
    hSMul iâ‚ iâ‚‚ (râ‚ + râ‚‚) m = hSMul iâ‚ iâ‚‚ râ‚ m + hSMul iâ‚ iâ‚‚ râ‚‚ m := by
  simp [hSMul, add_smul]

@[simp]
lemma zero_hSMul : hSMul iâ‚ iâ‚‚ (0 : â„›.obj câ‚) m = 0 := by
  simp [hSMul]

lemma hSMul_respect_Î¹
    {câ‚ câ‚‚ câ‚ƒ : J} (iâ‚ : câ‚ âŸ¶ câ‚ƒ) (iâ‚‚ : câ‚‚ âŸ¶ câ‚ƒ)
    (r : â„›.obj câ‚) (x : â„³.obj câ‚‚)
    {dâ‚ dâ‚‚ dâ‚ƒ : J} (jâ‚ : dâ‚ âŸ¶ dâ‚ƒ) (jâ‚‚ :  dâ‚‚ âŸ¶ dâ‚ƒ)
    (r' : â„›.obj dâ‚) (x' : â„³.obj dâ‚‚)
    (hrr' : colimit.Î¹ â„› _ r = colimit.Î¹ â„› _ r')
    (hmm' : colimit.Î¹ â„³ _ x = colimit.Î¹ â„³ _ x') :
    colimit.Î¹ â„³ _ (hSMul iâ‚ iâ‚‚ r x) =
    colimit.Î¹ â„³ _ (hSMul jâ‚ jâ‚‚ r' x') := by
  classical
  obtain âŸ¨m, fmâ‚, fmâ‚‚, hmâŸ© := Concrete.colimit_exists_of_rep_eq (h := hrr')
  obtain âŸ¨n, fnâ‚, fnâ‚‚, hnâŸ© := Concrete.colimit_exists_of_rep_eq (h := hmm')
  rw [Concrete.colimit_rep_eq_iff_exists]
  let O : Finset J := { câ‚, câ‚‚, câ‚ƒ, dâ‚, dâ‚‚, dâ‚ƒ, m, n }
  let H : Finset ((X : J) Ã—' (Y : J) Ã—' (_ : X âˆˆ O) Ã—' (_ : Y âˆˆ O) Ã—' (X âŸ¶ Y)) :=
  { âŸ¨câ‚, m, by simp [O], by simp [O], fmâ‚âŸ©,
    âŸ¨dâ‚, m, by simp [O], by simp [O], fmâ‚‚âŸ©,
    âŸ¨câ‚‚, n, by simp [O], by simp [O], fnâ‚âŸ©,
    âŸ¨dâ‚‚, n, by simp [O], by simp [O], fnâ‚‚âŸ©,
    âŸ¨câ‚, câ‚ƒ, by simp [O], by simp [O], iâ‚âŸ©,
    âŸ¨câ‚‚, câ‚ƒ, by simp [O], by simp [O], iâ‚‚âŸ©,
    âŸ¨dâ‚, dâ‚ƒ, by simp [O], by simp [O], jâ‚âŸ©,
    âŸ¨dâ‚‚, dâ‚ƒ, by simp [O], by simp [O], jâ‚‚âŸ© }

  let S := IsFiltered.sup O H

  refine âŸ¨S, IsFiltered.toSup O H (by simp [O]), IsFiltered.toSup _ _ (by simp [O]), ?_âŸ©
  delta hSMul
  rw [compatible_smul.out, compatible_smul.out]
  apply_fun â„›.map (IsFiltered.toSup O H (by simp [O])) at hm
  rw [â† comp_apply, â† comp_apply, â† â„›.map_comp, â† â„›.map_comp] at hm

  apply_fun â„³.map (IsFiltered.toSup O H (by simp [O])) at hn
  rw [â† comp_apply, â† comp_apply, â† â„³.map_comp, â† â„³.map_comp] at hn

  rw [â† comp_apply, â† comp_apply, â† comp_apply, â† comp_apply, â† â„›.map_comp, â† â„›.map_comp,
    â† â„³.map_comp, â† â„³.map_comp]
  convert congr($hm â€¢ $hn) using 1 <;> congr 3
  Â· rw [IsFiltered.toSup_commutes O H (f := iâ‚), IsFiltered.toSup_commutes O H (f := fmâ‚)]
    Â· simp only [Finset.mem_insert, Finset.mem_singleton, true_or, O]
    Â· simp only [Finset.mem_insert, PSigma.mk.injEq, heq_eq_eq, true_and, Finset.mem_singleton,
      true_or, O, H]
    Â· simp only [Finset.mem_insert, PSigma.mk.injEq, heq_eq_eq, true_and, Finset.mem_singleton,
      true_or, or_true, O, H]
  Â· rw [IsFiltered.toSup_commutes O H (f := iâ‚‚), IsFiltered.toSup_commutes O H (f := fnâ‚)]
    Â· simp only [Finset.mem_insert, Finset.mem_singleton, true_or, or_true, O]
    Â· simp only [Finset.mem_insert, PSigma.mk.injEq, heq_eq_eq, true_and, Finset.mem_singleton,
      true_or, or_true, O, H]
    Â· simp only [Finset.mem_insert, PSigma.mk.injEq, heq_eq_eq, true_and, Finset.mem_singleton,
      true_or, or_true, O, H]
  Â· rw [IsFiltered.toSup_commutes O H (f := jâ‚), IsFiltered.toSup_commutes O H (f := fmâ‚‚)]
    Â· simp only [Finset.mem_insert, Finset.mem_singleton, true_or, or_true, O]
    Â· simp only [Finset.mem_insert, PSigma.mk.injEq, heq_eq_eq, true_and, Finset.mem_singleton,
      true_or, or_true, O, H]
    Â· simp only [Finset.mem_insert, PSigma.mk.injEq, heq_eq_eq, true_and, Finset.mem_singleton,
      true_or, or_true, O, H]
  Â· rw [IsFiltered.toSup_commutes O H (f := jâ‚‚), IsFiltered.toSup_commutes O H (f := fnâ‚‚)]
    Â· simp only [Finset.mem_insert, Finset.mem_singleton, true_or, or_true, O]
    Â· simp only [Finset.mem_insert, PSigma.mk.injEq, Finset.mem_singleton, heq_eq_eq, true_and,
      true_or, or_true, O, H]
    Â· simp only [Finset.mem_insert, PSigma.mk.injEq, heq_eq_eq, true_and, Finset.mem_singleton,
      or_true, O, H]

end hSMul

variable {â„› â„³} in
/--
Let `R` be the filtered colimit of rings `{Râ±¼}` and `M` be the filtered colimit of
abelian groups `{Mâ±¼}` with the same indexing set `j âˆˆ J`, if for each `j âˆˆ J`, `Mâ±¼` is an
`Râ±¼`-module such that the `Râ±¼`-actions are compatible with the morphisms in `J`, then there is
a scalar multiplication `Râ±¼ â†’ M â†’ M` for every `j âˆˆ J`.
-/
noncomputable def sMulColimit {c : J} (r : â„›.obj c) (m : colimit (C := ğ”„ğ”Ÿ) â„³) :
    colimit (C := ğ”„ğ”Ÿ) â„³ :=
  colimit.Î¹ â„³ (IsFiltered.max c (Concrete.indexRepColimit â„³ m))
   (hSMul (IsFiltered.leftToMax _ _) (IsFiltered.rightToMax _ _)
    r (Concrete.repColimit â„³ m))

section sMulColimit

@[simp]
lemma sMulColimit_smul_rep (câ‚ câ‚‚ : J) (r : â„›.obj câ‚) (m : â„³.obj câ‚‚) :
    sMulColimit r (colimit.Î¹ â„³ câ‚‚ m) =
    colimit.Î¹ â„³ (IsFiltered.max câ‚ câ‚‚)
    (hSMul (IsFiltered.leftToMax _ _) (IsFiltered.rightToMax _ _) r m) := by
  apply hSMul_respect_Î¹
  Â· rfl
  Â· rw [Concrete.Î¹_repColimit_eq]

@[simp]
lemma sMulColimit_one_smul (c : J) (m : colimit (C := ğ”„ğ”Ÿ) â„³) :
    sMulColimit (1 : â„›.obj c) m = m := by
  rw [show m = colimit.Î¹ â„³ (Concrete.indexRepColimit â„³ m) _ by
    rw [Concrete.Î¹_repColimit_eq], sMulColimit_smul_rep, one_hSMul, colimit.w_apply]

lemma sMulColimit_mul_smul (c : J) (râ‚ râ‚‚ : â„›.obj c)
    (m : colimit (C := ğ”„ğ”Ÿ) â„³) :
    sMulColimit (râ‚ * râ‚‚) m = sMulColimit râ‚ (sMulColimit râ‚‚ m) := by
  simp only [show m = colimit.Î¹ â„³ (Concrete.indexRepColimit â„³ m) _ by
    rw [Concrete.Î¹_repColimit_eq], sMulColimit_smul_rep, mul_hSMul]
  apply hSMul_respect_Î¹
  Â· rfl
  Â· apply hSMul_respect_Î¹
    Â· rfl
    Â· rw [Concrete.Î¹_repColimit_eq]

@[simp]
lemma sMulColimit_smul_zero (c : J) (r : â„›.obj c) : sMulColimit (â„³ := â„³) r 0 = 0 := by
  rw [show (0 : colimit (C := ğ”„ğ”Ÿ) â„³) = colimit.Î¹ (C := ğ”„ğ”Ÿ) â„³ c 0 by rw [map_zero],
    sMulColimit_smul_rep, hSMul_zero, map_zero, map_zero]

lemma sMulColimit_smul_add (c : J) (r : â„›.obj c) (mâ‚ mâ‚‚ : colimit (C := ğ”„ğ”Ÿ) â„³) :
    sMulColimit r (mâ‚ + mâ‚‚) = sMulColimit r mâ‚ + sMulColimit r mâ‚‚ := by
  classical
  let O : Finset J :=
    { c, Concrete.indexRepColimit â„³ mâ‚, Concrete.indexRepColimit â„³ mâ‚‚ }
  let j : J := IsFiltered.sup O âˆ…

  have eqâ‚ : mâ‚ = colimit.Î¹ â„³ j
      (â„³.map (IsFiltered.toSup O âˆ… $ by simp [O]) (Concrete.repColimit â„³ mâ‚)) := by
    simp only [colimit.w_apply, Concrete.Î¹_repColimit_eq]
  have eqâ‚‚ : mâ‚‚ = colimit.Î¹ â„³ j
      (â„³.map (IsFiltered.toSup O âˆ… $ by simp [O]) (Concrete.repColimit â„³ mâ‚‚)) := by
    simp only [colimit.w_apply, Concrete.Î¹_repColimit_eq]
  have eqâ‚ƒ : mâ‚ + mâ‚‚ = colimit.Î¹ â„³ j
      (â„³.map (IsFiltered.toSup O âˆ… $ by simp [O]) (Concrete.repColimit â„³ mâ‚) +
       â„³.map (IsFiltered.toSup O âˆ… $ by simp [O]) (Concrete.repColimit â„³ mâ‚‚)) := by
    simp only [map_add, colimit.w_apply, Concrete.Î¹_repColimit_eq]

  rw [eqâ‚ƒ]
  conv_rhs => rw [eqâ‚]; rhs; rw [eqâ‚‚]
  rw [sMulColimit_smul_rep, sMulColimit_smul_rep, sMulColimit_smul_rep, hSMul_add, map_add]

lemma sMulColimit_add_smul (c : J) (râ‚ râ‚‚ : â„›.obj c) (m : colimit (C := ğ”„ğ”Ÿ) â„³) :
    sMulColimit (râ‚ + râ‚‚) m = sMulColimit râ‚ m + sMulColimit râ‚‚ m := by
  simp only [show m = colimit.Î¹ â„³ (Concrete.indexRepColimit â„³ m) _ by
    rw [Concrete.Î¹_repColimit_eq], sMulColimit_smul_rep, add_hSMul, map_add]

@[simp]
lemma sMulColimit_zero_smul (c : J) (m : colimit (C := ğ”„ğ”Ÿ) â„³) :
    sMulColimit (â„³ := â„³) (0 : â„›.obj c) m = 0 := by
  simp only [show m = colimit.Î¹ â„³ (Concrete.indexRepColimit â„³ m) _ by
    rw [Concrete.Î¹_repColimit_eq], sMulColimit_smul_rep, zero_hSMul, map_zero]

end sMulColimit

noncomputable instance moduleObjColimit (j : J) :
    Module (â„›.obj j) (colimit (C := ğ”„ğ”Ÿ) â„³) where
  smul := sMulColimit
  one_smul := sMulColimit_one_smul _ _ _
  mul_smul := sMulColimit_mul_smul _ _ _
  smul_zero := sMulColimit_smul_zero _ _ _
  smul_add := sMulColimit_smul_add _ _ _
  add_smul := sMulColimit_add_smul _ _ _
  zero_smul := sMulColimit_zero_smul _ _ _

variable {â„› â„³} in
/--
Let `R` be the filtered colimit of rings `{Râ±¼}` and `M` be the filtered colimit of
abelian groups `{Mâ±¼}`  with the same indexing category `J`. If for each `j âˆˆ J`, `Mâ±¼` is an
`Râ±¼`-module such that the `Râ±¼`-actions are compatible with the morphisms in `J`, then there is a
natural scalar multiplication `R â†’ M â†’ M`.
-/
noncomputable def colimitsMulColimit (r : colimit (C := â„œğ”¦ğ”«ğ”¤) â„›) (m : colimit (C := ğ”„ğ”Ÿ) â„³) :
    colimit (C := ğ”„ğ”Ÿ) â„³ :=
  (sMulColimit (Concrete.repColimit â„› r) m)

section colimitsMulColimit

@[simp]
lemma colimitsMulColimit_rep_smul {c : J} (r : â„›.obj c) (m : colimit (C := ğ”„ğ”Ÿ) â„³) :
    colimitsMulColimit (colimit.Î¹ â„› c r) m = sMulColimit r m := by
  rw [show m = colimit.Î¹ â„³ (Concrete.indexRepColimit â„³ m) _ by
    rw [Concrete.Î¹_repColimit_eq], sMulColimit_smul_rep]
  apply hSMul_respect_Î¹
  Â· rw [Concrete.Î¹_repColimit_eq]
  Â· rw [Concrete.Î¹_repColimit_eq, Concrete.Î¹_repColimit_eq]

@[simp]
lemma colimitsMulColimit_one_smul (m : colimit (C := ğ”„ğ”Ÿ) â„³) :
    colimitsMulColimit (1 : colimit (C := â„œğ”¦ğ”«ğ”¤) â„›) m = m := by
  let c : J := (inferInstance : IsFiltered J).2.some
  rw [show (1 : colimit (C := â„œğ”¦ğ”«ğ”¤) â„›) = colimit.Î¹ â„› c 1 by
    rw [map_one], colimitsMulColimit_rep_smul, sMulColimit_one_smul]

lemma colimitsMulColimit_mul_smul
    (râ‚ râ‚‚ : colimit (C := â„œğ”¦ğ”«ğ”¤) â„›) (m : colimit (C := ğ”„ğ”Ÿ) â„³) :
    colimitsMulColimit (râ‚ * râ‚‚) m = colimitsMulColimit râ‚ (colimitsMulColimit râ‚‚ m) := by
  classical
  let O : Finset J :=
    {  Concrete.indexRepColimit â„› râ‚, Concrete.indexRepColimit â„› râ‚‚ }
  let j : J := IsFiltered.sup O âˆ…
  have eqâ‚ : râ‚ = colimit.Î¹ â„› j
      (â„›.map (IsFiltered.toSup O âˆ… $ by simp [O]) (Concrete.repColimit â„› râ‚)) := by
    rw [colimit.w_apply, Concrete.Î¹_repColimit_eq]
  have eqâ‚‚ : râ‚‚ = colimit.Î¹ â„› j
      (â„›.map (IsFiltered.toSup O âˆ… $ by simp [O]) (Concrete.repColimit â„› râ‚‚)) := by
    rw [colimit.w_apply, Concrete.Î¹_repColimit_eq]
  have eqâ‚ƒ : râ‚ * râ‚‚ = colimit.Î¹ â„› j
      (â„›.map (IsFiltered.toSup O âˆ… $ by simp [O]) (Concrete.repColimit â„› râ‚) *
       â„›.map (IsFiltered.toSup O âˆ… $ by simp [O]) (Concrete.repColimit â„› râ‚‚)) := by
    rw [map_mul, colimit.w_apply, colimit.w_apply, Concrete.Î¹_repColimit_eq,
      Concrete.Î¹_repColimit_eq]
  rw [eqâ‚ƒ]
  conv_rhs => rw [eqâ‚]; rhs; rw [eqâ‚‚]
  rw [colimitsMulColimit_rep_smul, colimitsMulColimit_rep_smul, colimitsMulColimit_rep_smul,
    sMulColimit_mul_smul]

@[simp]
lemma colimitsMulColimit_smul_zero (r : colimit (C := â„œğ”¦ğ”«ğ”¤) â„›) :
    colimitsMulColimit (â„³ := â„³) r 0 = 0 := by
  rw [show r = colimit.Î¹ â„› (Concrete.indexRepColimit â„› r) _ by
    rw [Concrete.Î¹_repColimit_eq], colimitsMulColimit_rep_smul, sMulColimit_smul_zero]

lemma colimitsMulColimit_smul_add (r : colimit (C := â„œğ”¦ğ”«ğ”¤) â„›) (mâ‚ mâ‚‚ : colimit (C := ğ”„ğ”Ÿ) â„³) :
    colimitsMulColimit r (mâ‚ + mâ‚‚) = colimitsMulColimit r mâ‚ + colimitsMulColimit r mâ‚‚ := by
  simp only [show r = colimit.Î¹ â„› (Concrete.indexRepColimit â„› r) _ by
      rw [Concrete.Î¹_repColimit_eq],
    colimitsMulColimit_rep_smul, sMulColimit_smul_add]

lemma colimitsMulColimit_add_smul (râ‚ râ‚‚ : colimit (C := â„œğ”¦ğ”«ğ”¤) â„›) (m : colimit (C := ğ”„ğ”Ÿ) â„³) :
    colimitsMulColimit (râ‚ + râ‚‚) m = colimitsMulColimit râ‚ m + colimitsMulColimit râ‚‚ m := by
  classical
  let O : Finset J :=
    {  Concrete.indexRepColimit â„› râ‚, Concrete.indexRepColimit â„› râ‚‚ }
  let j : J := IsFiltered.sup O âˆ…
  have eqâ‚ : râ‚ = colimit.Î¹ â„› j
      (â„›.map (IsFiltered.toSup O âˆ… $ by simp [O]) (Concrete.repColimit â„› râ‚)) := by
    rw [colimit.w_apply, Concrete.Î¹_repColimit_eq]
  have eqâ‚‚ : râ‚‚ = colimit.Î¹ â„› j
      (â„›.map (IsFiltered.toSup O âˆ… $ by simp [O]) (Concrete.repColimit â„› râ‚‚)) := by
    rw [colimit.w_apply, Concrete.Î¹_repColimit_eq]
  have eqâ‚ƒ : râ‚ + râ‚‚ = colimit.Î¹ â„› j
      (â„›.map (IsFiltered.toSup O âˆ… $ by simp [O]) (Concrete.repColimit â„› râ‚) +
       â„›.map (IsFiltered.toSup O âˆ… $ by simp [O]) (Concrete.repColimit â„› râ‚‚)) := by
    rw [map_add]
    rw [colimit.w_apply, colimit.w_apply, Concrete.Î¹_repColimit_eq, Concrete.Î¹_repColimit_eq]
  rw [eqâ‚ƒ]
  conv_rhs => rw [eqâ‚]; rhs; rw [eqâ‚‚]
  rw [colimitsMulColimit_rep_smul, colimitsMulColimit_rep_smul, colimitsMulColimit_rep_smul,
    sMulColimit_add_smul]

@[simp]
lemma colimitsMulColimit_zero_smul (m : colimit (C := ğ”„ğ”Ÿ) â„³) :
    colimitsMulColimit (0 : colimit (C := â„œğ”¦ğ”«ğ”¤) â„›) m = 0 := by
  let c : J := (inferInstance : IsFiltered J).2.some
  rw [show (0 : colimit (C := â„œğ”¦ğ”«ğ”¤) â„›) = colimit.Î¹ â„› c 0 by rw [map_zero],
    colimitsMulColimit_rep_smul, sMulColimit_zero_smul]

end colimitsMulColimit

noncomputable instance moduleColimitColimit :
    Module (colimit (C := â„œğ”¦ğ”«ğ”¤) â„›) (colimit (C := ğ”„ğ”Ÿ) â„³) where
  smul := colimitsMulColimit
  one_smul := colimitsMulColimit_one_smul _ _
  mul_smul := colimitsMulColimit_mul_smul _ _
  smul_zero := colimitsMulColimit_smul_zero _ _
  smul_add := colimitsMulColimit_smul_add _ _
  add_smul := colimitsMulColimit_add_smul _ _
  zero_smul := colimitsMulColimit_zero_smul _ _

lemma smul_spec
    (jâ‚ jâ‚‚ jâ‚ƒ : J) (iâ‚ : jâ‚ âŸ¶ jâ‚ƒ) (iâ‚‚ : jâ‚‚ âŸ¶ jâ‚ƒ)
    (s : â„›.obj jâ‚) (t : â„³.obj jâ‚‚):
    colimit.Î¹ â„› jâ‚ s â€¢ colimit.Î¹ â„³ jâ‚‚ t = colimit.Î¹ â„³ jâ‚ƒ (â„›.map iâ‚ s â€¢ â„³.map iâ‚‚ t) :=
  show colimitsMulColimit _ _ = colimit.Î¹ â„³ jâ‚ƒ (â„›.map iâ‚ s â€¢ â„³.map iâ‚‚ t) by
    rw [colimitsMulColimit_rep_smul, sMulColimit_smul_rep]
    apply hSMul_respect_Î¹ <;> rfl

end Module.overFilteredColimits
