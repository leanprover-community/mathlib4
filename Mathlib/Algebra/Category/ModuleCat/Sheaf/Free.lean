/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Category.ModuleCat.Presheaf.Colimits
import Mathlib.Algebra.Category.ModuleCat.Sheaf.Colimits

/-!
# Free sheaves of modules

In this file, we construct the functor
`SheafOfModules.freeFunctor : Type u ‚•§ SheafOfModules.{u} R` which sends
a type `I` to the coproduct of copies indexed by `I` of `unit R`.

## TODO

* In case the category `C` has a terminal object `X`, promote `freeHomEquiv`
  into an adjunction between `freeFunctor` and the evaluation functor at `X`.
  (Alternatively, assuming specific universe parameters, we could show that
  `freeHomEquiv` is a left adjoint to `SheafOfModules.sectionsFunctor`.)

-/

universe u v' u'
open CategoryTheory Limits

variable {C : Type u'} [Category.{v'} C] {J : GrothendieckTopology C} {R : Sheaf J RingCat.{u}}
  [HasWeakSheafify J AddCommGrpCat.{u}] [J.WEqualsLocallyBijective AddCommGrpCat.{u}]
  [J.HasSheafCompose (forget‚ÇÇ RingCat.{u} AddCommGrpCat.{u})]

namespace SheafOfModules

/-- The free sheaf of modules on a certain type `I`. -/
noncomputable def free (I : Type u) : SheafOfModules.{u} R := ‚àê (fun (_ : I) ‚Ü¶ unit R)

/-- The inclusions `unit R ‚ü∂ free I`. -/
noncomputable def ŒπFree {I : Type u} (i : I) : unit R ‚ü∂ free I :=
  Sigma.Œπ (fun (_ : I) ‚Ü¶ unit R) i

/-- The tautological cofan with point `free I : SheafOfModules R`. -/
noncomputable def freeCofan (I : Type u) : Cofan (fun (_ : I) ‚Ü¶ unit R) :=
  Cofan.mk (P := free I) (fun i ‚Ü¶ ŒπFree i)

@[simp]
lemma freeCofan_inj {I : Type u} (i : I) :
    (freeCofan (R := R) I).inj i = ŒπFree i := rfl

/-- `free I` is the colimit of copies of `unit R` indexed by `I`. -/
noncomputable def isColimitFreeCofan (I : Type u) :
    IsColimit (freeCofan (R := R) I) :=
  coproductIsCoproduct _

/-- The data of a morphism `free I ‚ü∂ M` from a free sheaf of modules is
equivalent to the data of a family `I ‚Üí M.sections` of sections of `M`. -/
noncomputable def freeHomEquiv (M : SheafOfModules.{u} R) {I : Type u} :
    (free I ‚ü∂ M) ‚âÉ (I ‚Üí M.sections) where
  toFun f i := M.unitHomEquiv (ŒπFree i ‚â´ f)
  invFun s := Cofan.IsColimit.desc (isColimitFreeCofan I) (fun i ‚Ü¶ M.unitHomEquiv.symm (s i))
  left_inv s := Cofan.IsColimit.hom_ext (isColimitFreeCofan I) _ _ (fun i ‚Ü¶ by
    simp only [freeCofan_inj, Equiv.symm_apply_apply]
    apply Cofan.IsColimit.fac)
  right_inv f := by
    ext1 i
    dsimp
    apply M.unitHomEquiv.symm.injective
    simp only [Equiv.symm_apply_apply]
    apply Cofan.IsColimit.fac

lemma freeHomEquiv_comp_apply {M N : SheafOfModules.{u} R} {I : Type u}
    (f : free I ‚ü∂ M) (p : M ‚ü∂ N) (i : I) :
    N.freeHomEquiv (f ‚â´ p) i = sectionsMap p (M.freeHomEquiv f i) := rfl

lemma freeHomEquiv_symm_comp {M N : SheafOfModules.{u} R} {I : Type u} (s : I ‚Üí M.sections)
    (p : M ‚ü∂ N) :
    M.freeHomEquiv.symm s ‚â´ p = N.freeHomEquiv.symm (fun i ‚Ü¶ sectionsMap p (s i)) :=
  N.freeHomEquiv.injective (by ext; simp [freeHomEquiv_comp_apply])

/-- The tautological section of `free I : SheafOfModules R` corresponding to `i : I`. -/
noncomputable abbrev freeSection {I : Type u} (i : I) : (free (R := R) I).sections :=
  (free (R := R) I).freeHomEquiv (ùüô (free I)) i

lemma unitHomEquiv_symm_freeHomEquiv_apply
    {I : Type u} {M : SheafOfModules.{u} R} (f : free I ‚ü∂ M) (i : I) :
    M.unitHomEquiv.symm (M.freeHomEquiv f i) = ŒπFree i ‚â´ f := by
  simp [freeHomEquiv]

section

variable {I J : Type u} (f : I ‚Üí J)

/-- The morphism of presheaves of `R`-modules `free I ‚ü∂ free J` induced by
a map `f : I ‚Üí J`. -/
noncomputable def freeMap : free (R := R) I ‚ü∂ free J :=
  (freeHomEquiv _).symm (fun i ‚Ü¶ freeSection (f i))

@[simp]
lemma freeHomEquiv_freeMap :
    (freeHomEquiv _ (freeMap (R := R) f)) = freeSection.comp f :=
  (freeHomEquiv _).symm.injective (by simp; rfl)

@[simp]
lemma sectionMap_freeMap_freeSection (i : I) :
    sectionsMap (freeMap (R := R) f) (freeSection i) = freeSection (f i) := by
  simp [‚Üê freeHomEquiv_comp_apply]

@[reassoc (attr := simp)]
lemma ŒπFree_freeMap (i : I) :
    ŒπFree (R := R) i ‚â´ freeMap f = ŒπFree (f i) := by
  rw [‚Üê unitHomEquiv_symm_freeHomEquiv_apply, freeHomEquiv_freeMap]
  dsimp [freeSection]
  rw [unitHomEquiv_symm_freeHomEquiv_apply, Category.comp_id]

end

/-- The functor `Type u ‚•§ SheafOfModules.{u} R` which sends a type `I` to
`free I` which is a coproduct indexed by `I` of copies of `R` (thought of as a
presheaf of modules over itself). -/
@[simps]
noncomputable def freeFunctor : Type u ‚•§ SheafOfModules.{u} R where
  obj := free
  map f := freeMap f
  map_id X := (freeHomEquiv _).injective (by ext1 i; simp)
  map_comp {I J K} f g := (freeHomEquiv _).injective (by ext1; simp [freeHomEquiv_comp_apply])

end SheafOfModules
