/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.Algebra.Category.ModuleCat.Sheaf.Generators
public import Mathlib.Algebra.Category.ModuleCat.Sheaf.Abelian
public import Mathlib.CategoryTheory.FiberedCategory.HomLift
public import Mathlib.CategoryTheory.Comma.Over.Pullback
public import Mathlib.CategoryTheory.Equivalence.Symmetry

/-!
# Quasicoherent sheaves

A sheaf of modules is quasi-coherent if it admits locally a presentation as the
cokernel of a morphism between coproducts of copies of the sheaf of rings.
When these coproducts are finite, we say that the sheaf is of finite presentation.

## References

* https://stacks.math.columbia.edu/tag/01BD

-/

@[expose] public section

universe u v' u'

open CategoryTheory Limits

variable {C : Type u'} [Category.{v'} C] {J : GrothendieckTopology C}
  {R : Sheaf J RingCat.{u}}

namespace SheafOfModules

section

variable [HasWeakSheafify J AddCommGrpCat.{u}] [J.WEqualsLocallyBijective AddCommGrpCat.{u}]
  [J.HasSheafCompose (forget‚ÇÇ RingCat.{u} AddCommGrpCat.{u})]

/-- A global presentation of a sheaf of modules `M` consists of a family `generators.s`
of sections `s` which generate `M`, and a family of sections which generate
the kernel of the morphism `generators.œÄ : free (generators.I) ‚ü∂ M`. -/
structure Presentation (M : SheafOfModules.{u} R) where
  /-- generators -/
  generators : M.GeneratingSections
  /-- relations -/
  relations : (kernel generators.œÄ).GeneratingSections

/-- A global presentation of a sheaf of module if finite if the type
of generators and relations are finite. -/
class Presentation.IsFinite {M : SheafOfModules.{u} R} (p : M.Presentation) : Prop where
  isFiniteType_generators : p.generators.IsFiniteType := by infer_instance
  finite_relations : Finite p.relations.I := by infer_instance

attribute [instance] Presentation.IsFinite.isFiniteType_generators
  Presentation.IsFinite.finite_relations

end

noncomputable section

variable {C : Type u'} [Category.{v'} C] {J : GrothendieckTopology C} {R : Sheaf J RingCat}
  [HasSheafify J AddCommGrpCat] [J.WEqualsLocallyBijective AddCommGrpCat]
  [J.HasSheafCompose (forget‚ÇÇ RingCat AddCommGrpCat)] {Œπ œÉ : Type u}

/-- Given two morphisms of sheaves of `R`-modules `f : free Œπ ‚ü∂ free œÉ` and `g : free œÉ ‚ü∂ M`
satisfying `H : f ‚â´ g = 0` and `IsColimit (CokernelCofork.ofœÄ g H)`, we obtain
generators of `Presentation M`. -/
@[simps! I s]
def generatorsOfIsCokernelFree {M : SheafOfModules.{u} R}
    (f : free Œπ ‚ü∂ free œÉ) (g : free œÉ ‚ü∂ M) (H : f ‚â´ g = 0)
    (H' : IsColimit (CokernelCofork.ofœÄ g H)) : M.GeneratingSections where
  I := œÉ
  s := M.freeHomEquiv g
  epi := by simpa using epi_of_isColimit_cofork H'

@[simp]
theorem generatorsOfIsCokernelFree_œÄ {M : SheafOfModules.{u} R}
    (f : free Œπ ‚ü∂ free œÉ) (g : free œÉ ‚ü∂ M) (H : f ‚â´ g = 0)
    (H' : IsColimit (CokernelCofork.ofœÄ g H)) :
    (generatorsOfIsCokernelFree f g H H').œÄ = g := M.freeHomEquiv.symm_apply_apply g

/-- Given two morphisms of sheaves of `R`-modules `f : free Œπ ‚ü∂ free œÉ` and `g : free œÉ ‚ü∂ M`
satisfying `H : f ‚â´ g = 0` and `IsColimit (CokernelCofork.ofœÄ g H)`, we obtain
relations of `Presentation M`. -/
@[simps! I s]
def relationsOfIsCokernelFree {M : SheafOfModules.{u} R}
    (f : free Œπ ‚ü∂ free œÉ) (g : free œÉ ‚ü∂ M) (H : f ‚â´ g = 0)
    (H' : IsColimit (CokernelCofork.ofœÄ g H)) :
    (kernel (generatorsOfIsCokernelFree f g H H').œÄ).GeneratingSections where
  I := Œπ
  s := (kernel (generatorsOfIsCokernelFree f g H H').œÄ).freeHomEquiv <| kernel.lift
    (generatorsOfIsCokernelFree f g H H').œÄ f (by simp [H])
  epi := by
    let h : cokernel f ‚âÖ M := (H'.coconePointUniqueUpToIso (colimit.isColimit _)).symm
    let h' : Abelian.image f ‚âÖ kernel (generatorsOfIsCokernelFree f g H H').œÄ :=
      kernel.mapIso (cokernel.œÄ f) (generatorsOfIsCokernelFree f g H H').œÄ
        (Iso.refl _) h (by simp [h])
    have comp_aux : Abelian.factorThruImage f ‚â´ h'.hom =
      (kernel.lift (generatorsOfIsCokernelFree f g H H').œÄ f (by simp [H])) :=
        equalizer.hom_ext <| by simp [h']
    rw [‚Üê comp_aux, Equiv.symm_apply_apply]
    infer_instance

/-- Given two morphisms of sheaves of `R`-modules `f : free Œπ ‚ü∂ free œÉ` and `g : free œÉ ‚ü∂ M`
satisfying `H : f ‚â´ g = 0` and `IsColimit (CokernelCofork.ofœÄ g H)`, we obtain a
`Presentation M`. -/
@[simps]
def presentationOfIsCokernelFree {M : SheafOfModules.{u} R}
    (f : free Œπ ‚ü∂ free œÉ) (g : free œÉ ‚ü∂ M) (H : f ‚â´ g = 0)
    (H' : IsColimit (CokernelCofork.ofœÄ g H)) : Presentation M where
  generators := generatorsOfIsCokernelFree f g H H'
  relations := relationsOfIsCokernelFree f g H H'

/-- Given a sheaf of `R`-modules `M` and a `Presentation M`, there is two morphism of
sheaves of `R`-modules `f : free Œπ ‚ü∂ free œÉ` and `g : free œÉ ‚ü∂ M` satisfying `H : f ‚â´ g = 0`
and `IsColimit (CokernelCofork.ofœÄ g H)`. -/
def Presentation.isColimit {M : SheafOfModules.{u} R} (P : Presentation M) :
    IsColimit (CokernelCofork.ofœÄ (f := (freeHomEquiv _).symm P.relations.s ‚â´ (kernel.Œπ _))
      P.generators.œÄ (by simp)) :=
  isCokernelEpiComp (c := CokernelCofork.ofœÄ _ (kernel.condition P.generators.œÄ))
      (Abelian.epiIsCokernelOfKernel _ <| limit.isLimit _) _ rfl

variable {C' : Type u'} [Category.{v'} C'] {J' : GrothendieckTopology C'} {S : Sheaf J' RingCat}
  [HasSheafify J' AddCommGrpCat] [J'.WEqualsLocallyBijective AddCommGrpCat]
  [J'.HasSheafCompose (forget‚ÇÇ RingCat AddCommGrpCat)]

variable {M : SheafOfModules.{u'} R} (P : Presentation M)
  (F : SheafOfModules.{u'} R ‚•§ SheafOfModules.{u'} S) [PreservesColimits F]
  (Œ∑ : F.obj (unit R) ‚âÖ unit S)

/-- Let `F` be a functor from sheaf of `R`-module to sheaf of `S`-module, if `F` preserves
colimits and `F.obj (unit R) ‚âÖ unit S`, given a `P : Presentation M`, then we will obtain
relations of `Presentation (F.obj M)`. -/
def Presentation.mapRelations : free P.relations.I (R := S) ‚ü∂ free P.generators.I :=
  (mapFree F Œ∑ P.relations.I).inv ‚â´ F.map ((freeHomEquiv _).symm P.relations.s) ‚â´
    F.map (kernel.Œπ _) ‚â´ (mapFree F Œ∑ P.generators.I).hom

/-- Let `F` be a functor from sheaf of `R`-module to sheaf of `S`-module, if `F` preserves
colimits and `F.obj (unit R) ‚âÖ unit S`, given a `P : Presentation M`, then we will obtain
generators of `Presentation (F.obj M)`. -/
def Presentation.mapGenerators : free P.generators.I ‚ü∂ F.obj M :=
  (mapFree F Œ∑ P.generators.I).inv ‚â´ F.map (P.generators.œÄ)

@[reassoc (attr := simp)]
theorem Presentation.mapRelations_mapGenerators :
    P.mapRelations F Œ∑ ‚â´ P.mapGenerators F Œ∑ = 0 := by
  simp only [mapRelations, mapGenerators, Category.assoc, Iso.hom_inv_id_assoc,
    ‚Üê Functor.map_comp, kernel.condition, Functor.map_zero, comp_zero]

/-- Let `F` be a functor from sheaf of `R`-module to sheaf of `S`-module, if `F` preserves
colimits and `F.obj (unit R) ‚âÖ unit S`, given a `P : Presentation M`, then we will get a
`Presentation (F.obj M)`. -/
@[simps! generators_I relations_I]
def Presentation.map : Presentation (F.obj M) :=
  presentationOfIsCokernelFree (P.mapRelations F Œ∑) (P.mapGenerators F Œ∑)
    (P.mapRelations_mapGenerators F Œ∑) <| by
    refine IsColimit.equivOfNatIsoOfIso (parallelPairIsoMk (mapFree F Œ∑ _) (mapFree F Œ∑ _)
      (by simp [Presentation.mapRelations]) (by simp)) _ _ ?_ (isColimitOfPreserves F P.isColimit)
    exact (Cocones.ext (Iso.refl _) <| by rintro (_ | _)
      <;> simp [Presentation.mapRelations, Presentation.mapGenerators, ‚Üê Functor.map_comp])

theorem Presentation.map_œÄ_eq :
    (P.map F Œ∑).generators.œÄ = (mapFree F Œ∑ _).inv ‚â´ F.map (P.generators.œÄ) :=
  (F.obj M).freeHomEquiv.symm_apply_eq.mpr rfl

end

section

variable [‚àÄ X, (J.over X).HasSheafCompose (forget‚ÇÇ RingCat.{u} AddCommGrpCat.{u})]
  [‚àÄ X, HasWeakSheafify (J.over X) AddCommGrpCat.{u}]
  [‚àÄ X, (J.over X).WEqualsLocallyBijective AddCommGrpCat.{u}]

/-- This structure contains the data of a family of objects `X i` which cover
the terminal object, and of a presentation of `M.over (X i)` for all `i`. -/
structure QuasicoherentData (M : SheafOfModules.{u} R) where
  /-- the index type of the covering -/
  I : Type u'
  /-- a family of objects which cover the terminal object -/
  X : I ‚Üí C
  coversTop : J.CoversTop X
  /-- a presentation of the sheaf of modules `M.over (X i)` for any `i : I` -/
  presentation (i : I) : (M.over (X i)).Presentation

namespace QuasicoherentData

/-- If `M` is quasicoherent, it is locally generated by sections. -/
@[simps]
def localGeneratorsData {M : SheafOfModules.{u} R} (q : M.QuasicoherentData) :
    M.LocalGeneratorsData where
  I := q.I
  X := q.X
  coversTop := q.coversTop
  generators i := (q.presentation i).generators

/-- A (local) presentation of a sheaf of module `M` is a finite presentation
if each given presentation of `M.over (X i)` is a finite presentation. -/
class IsFinitePresentation {M : SheafOfModules.{u} R} (q : M.QuasicoherentData) : Prop where
  isFinite_presentation (i : q.I) : (q.presentation i).IsFinite := by infer_instance

attribute [instance] IsFinitePresentation.isFinite_presentation

instance {M : SheafOfModules.{u} R} (q : M.QuasicoherentData) [q.IsFinitePresentation] :
    q.localGeneratorsData.IsFiniteType where
  isFiniteType := by dsimp; infer_instance

end QuasicoherentData

/-- A sheaf of modules is quasi-coherent if it is locally the cokernel of a
morphism between coproducts of copies of the sheaf of rings. -/
class IsQuasicoherent (M : SheafOfModules.{u} R) : Prop where
  nonempty_quasicoherentData : Nonempty M.QuasicoherentData := by infer_instance

variable (R) in
@[inherit_doc IsQuasicoherent]
abbrev isQuasicoherent : ObjectProperty (SheafOfModules.{u} R) :=
  IsQuasicoherent

/-- A sheaf of modules is finitely presented if it is locally the cokernel of a
morphism between coproducts of finitely many copies of the sheaf of rings. -/
class IsFinitePresentation (M : SheafOfModules.{u} R) : Prop where
  exists_quasicoherentData (M) :
    ‚àÉ (œÉ : M.QuasicoherentData), œÉ.IsFinitePresentation

variable (R) in
@[inherit_doc IsFinitePresentation]
abbrev isFinitePresentation : ObjectProperty (SheafOfModules.{u} R) :=
  IsFinitePresentation

instance (M : SheafOfModules.{u} R) [M.IsFinitePresentation] :
    M.IsQuasicoherent where
  nonempty_quasicoherentData :=
    ‚ü®(IsFinitePresentation.exists_quasicoherentData M).choose‚ü©

instance (M : SheafOfModules.{u} R) [M.IsFinitePresentation] :
    M.IsFiniteType where
  exists_localGeneratorsData := by
    obtain ‚ü®œÉ, _‚ü© := IsFinitePresentation.exists_quasicoherentData M
    exact ‚ü®œÉ.localGeneratorsData, inferInstance‚ü©

/-- A choice of local presentations when `M` is a sheaf of modules of finite presentation. -/
@[deprecated "Use the lemma `IsFinitePresentation.exists_quasicoherentData` instead."
  (since := "2025-10-28")]
noncomputable def quasicoherentDataOfIsFinitePresentation
    (M : SheafOfModules.{u} R) [M.IsFinitePresentation] : M.QuasicoherentData :=
  (IsFinitePresentation.exists_quasicoherentData M).choose

end

section

open CategoryTheory Limits

@[simp]
lemma _root_.CategoryTheory.Over.forgetAdjStar_counit
    {C : Type u} [Category C] (X Y : C) [HasBinaryProducts C] :
    (Over.forgetAdjStar X).counit.app Y = prod.snd := by
  simp [Over.forgetAdjStar, CategoryTheory.coalgebraEquivOver]

@[simp]
lemma _root_.CategoryTheory.Over.forgetAdjStar_unit
    {C : Type u} [Category C] (X : C) (Y : Over X) [HasBinaryProducts C] :
    ((Over.forgetAdjStar X).unit.app Y).left = prod.lift Y.hom (ùüô _) := by
  simp [Over.forgetAdjStar, CategoryTheory.coalgebraEquivOver]

variable {C : Type u'} [SmallCategory C] [HasBinaryProducts C] {J : GrothendieckTopology C}
  {R : Sheaf J RingCat} [HasSheafify J AddCommGrpCat] [J.WEqualsLocallyBijective AddCommGrpCat]
  [J.HasSheafCompose (forget‚ÇÇ RingCat AddCommGrpCat)]

variable [‚àÄ X, (J.over X).HasSheafCompose (forget‚ÇÇ RingCat AddCommGrpCat)]
  [‚àÄ X, HasSheafify (J.over X) AddCommGrpCat]
  [‚àÄ X, (J.over X).WEqualsLocallyBijective AddCommGrpCat]

-- noncomputable
-- def Presentation.QuasicoherentData {M : SheafOfModules.{u'} R} (P : Presentation M) :
--   QuasicoherentData M where
--   I := C
--   X := id
--   coversTop := fun x ‚Ü¶ GrothendieckTopology.covering_of_eq_top J <| by
--     rw [Sieve.ext_iff]
--     intro _ f
--     simp only [Sieve.top_apply, iff_true]
--     use x, f
--   presentation := by
--     refine fun x ‚Ü¶ @Presentation.map.{u'} C _ J R _ _ _ (Over x) _ _ (R.over x) _ _ _  M P
--       (pushforward.{u'} (ùüô _)) ?_ (by rfl)
--     let œà : R ‚ü∂ ((Over.star x).sheafPushforwardContinuous RingCat J (J.over x)).obj (R.over x) :=
--       ‚ü®{app U := R.val.map Limits.prod.snd.op
--         naturality U V f := by simp [‚Üê Functor.map_comp, ‚Üê op_comp]; rfl }‚ü©
--     have Adj_aux : pushforward.{u'} (ùüô (R.over x)) ‚ä£ pushforward.{u'} œà := by
--       refine pushforwardPushforwardAdj (Over.forgetAdjStar x) (ùüô (R.over x)) œà ?_ ?_
--       ¬∑ ext y : 2
--         simp [œà]
--       ¬∑ ext y : 2
--         simp [œà, ‚Üê Functor.map_comp, ‚Üê op_comp]
--     refine Adjunction.leftAdjoint_preservesColimits Adj_aux

-- lemma Presentation.isQuasicoherent {M : SheafOfModules.{u'} R} (P : Presentation M) :
--     IsQuasicoherent M where
--   nonempty_quasicoherentData := Nonempty.intro (Presentation.QuasicoherentData P)


end

end SheafOfModules
