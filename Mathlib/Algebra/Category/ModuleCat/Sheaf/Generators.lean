/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import Mathlib.Algebra.Category.ModuleCat.Sheaf.Free
import Mathlib.Algebra.Category.ModuleCat.Sheaf.PushforwardContinuous
import Mathlib.CategoryTheory.Sites.CoversTop

/-!
# Generating sections of sheaves of modules

In this file, given a sheaf of modules `M` over a sheaf of rings `R`, we introduce
the structure `M.GeneratingSections` which consists of a family of (global)
sections `s : I â†’ M.sections` which generate `M`.

We also introduce the structure `M.LocalGeneratorsData` which contains the data
of a covering `X i` of the terminal object and the data of a
`(M.over (X i)).GeneratingSections` for all `i`. This is used in order to
define sheaves of modules of finite type.

## References

* https://stacks.math.columbia.edu/tag/01B4

-/

universe u v' u'

open CategoryTheory Limits

variable {C : Type u'} [Category.{v'} C] {J : GrothendieckTopology C} {R : Sheaf J RingCat.{u}}
  [HasWeakSheafify J AddCommGrpCat.{u}] [J.WEqualsLocallyBijective AddCommGrpCat.{u}]
  [J.HasSheafCompose (forgetâ‚‚ RingCat.{u} AddCommGrpCat.{u})]

namespace SheafOfModules

variable (M N P : SheafOfModules.{u} R)

/-- The type of sections which generate a sheaf of modules. -/
structure GeneratingSections where
  /-- the index type for the sections -/
  I : Type u
  /-- a family of sections which generate the sheaf of modules -/
  s : I â†’ M.sections
  epi : Epi (M.freeHomEquiv.symm s) := by infer_instance

namespace GeneratingSections

attribute [instance] epi

variable {M N P}

/-- The epimorphism `free Ïƒ.I âŸ¶ M` given by `Ïƒ : M.GeneratingSections`. -/
noncomputable abbrev Ï€ (Ïƒ : M.GeneratingSections) : free Ïƒ.I âŸ¶ M := M.freeHomEquiv.symm Ïƒ.s

/-- If `M âŸ¶ N` is an epimorphism and that `M` is generated by some sections,
then `N` is generated by the images of these sections. -/
@[simps]
def ofEpi (Ïƒ : M.GeneratingSections) (p : M âŸ¶ N) [Epi p] :
    N.GeneratingSections where
  I := Ïƒ.I
  s i := sectionsMap p (Ïƒ.s i)
  epi := by
    rw [â† freeHomEquiv_symm_comp]
    apply epi_comp

lemma opEpi_id (Ïƒ : M.GeneratingSections) :
    Ïƒ.ofEpi (ğŸ™ M) = Ïƒ := rfl

lemma opEpi_comp (Ïƒ : M.GeneratingSections) (p : M âŸ¶ N) (q : N âŸ¶ P) [Epi p] [Epi q] :
    Ïƒ.ofEpi (p â‰« q) = (Ïƒ.ofEpi p).ofEpi q := rfl

/-- Two isomorphic sheaves of modules have equivalent families of generating sections. -/
def equivOfIso (e : M â‰… N) :
    M.GeneratingSections â‰ƒ N.GeneratingSections where
  toFun Ïƒ := Ïƒ.ofEpi e.hom
  invFun Ïƒ := Ïƒ.ofEpi e.inv
  left_inv Ïƒ := by
    dsimp
    simp only [â† opEpi_comp, e.hom_inv_id, opEpi_id]
  right_inv Ïƒ := by
    dsimp
    simp only [â† opEpi_comp, e.inv_hom_id, opEpi_id]

/-- `Ïƒ : M.GeneratingSections` is finite type if the index type `Ïƒ.I` is finite. -/
class IsFiniteType (Ïƒ : M.GeneratingSections) : Prop where
  finite : Finite Ïƒ.I := by infer_instance

attribute [instance] IsFiniteType.finite

end GeneratingSections

variable [âˆ€ (X : C), HasWeakSheafify (J.over X) AddCommGrpCat.{u}]
  [âˆ€ (X : C), (J.over X).WEqualsLocallyBijective AddCommGrpCat.{u}]
  [âˆ€ (X : C), (J.over X).HasSheafCompose (forgetâ‚‚ RingCat AddCommGrpCat.{u})]

/-- The data of generating sections of the restriction of a sheaf of modules
over a covering of the terminal object. -/
structure LocalGeneratorsData where
  /-- the index type of the covering -/
  I : Type u'
  /-- a family of objects which cover the terminal object -/
  X : I â†’ C
  coversTop : J.CoversTop X
  /-- the data of sections of `M` over `X i` which generate `M.over (X i)` -/
  generators (i : I) : (M.over (X i)).GeneratingSections

variable {M} in
/-- The data of local generators of a sheaf of modules is finite type if
each family of generators is finite type. -/
class LocalGeneratorsData.IsFiniteType (p : M.LocalGeneratorsData) : Prop where
  isFiniteType (i : p.I) : (p.generators i).IsFiniteType := by infer_instance

/-- A sheaf of modules is of finite type if locally, it is generated by finitely
many sections. -/
class IsFiniteType (M : SheafOfModules.{u} R) : Prop where
  exists_localGeneratorsData (M) :
    âˆƒ (Ïƒ : M.LocalGeneratorsData), Ïƒ.IsFiniteType

end SheafOfModules
