/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.Algebra.Category.ModuleCat.Presheaf.Pushforward
public import Mathlib.Algebra.Category.ModuleCat.Sheaf
public import Mathlib.CategoryTheory.Sites.Over

/-!
# Pushforward of sheaves of modules

Assume that categories `C` and `D` are equipped with Grothendieck topologies, and
that `F : C ‚•§ D` is a continuous functor.
Then, if `œÜ : S ‚ü∂ (F.sheafPushforwardContinuous RingCat.{u} J K).obj R` is
a morphism of sheaves of rings, we construct the pushforward functor
`pushforward œÜ : SheafOfModules.{v} R ‚•§ SheafOfModules.{v} S`.

-/

@[expose] public section

universe v' u' v v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ u

open CategoryTheory

namespace SheafOfModules

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
  {J : GrothendieckTopology C} {K : GrothendieckTopology D} {F : C ‚•§ D}
  {S : Sheaf J RingCat.{u}} {R : Sheaf K RingCat.{u}}
  [Functor.IsContinuous.{u} F J K] [Functor.IsContinuous.{v} F J K]
  (œÜ : S ‚ü∂ (F.sheafPushforwardContinuous RingCat.{u} J K).obj R)

/-- The pushforward of sheaves of modules that is induced by a continuous functor `F`
and a morphism of sheaves of rings `œÜ : S ‚ü∂ (F.sheafPushforwardContinuous RingCat J K).obj R`. -/
@[simps map_val, simps -isSimp obj_val]
noncomputable def pushforward : SheafOfModules.{v} R ‚•§ SheafOfModules.{v} S where
  obj M :=
    { val := (PresheafOfModules.pushforward œÜ.val).obj M.val
      isSheaf := ((F.sheafPushforwardContinuous _ J K).obj ‚ü®_, M.isSheaf‚ü©).cond }
  map f :=
    { val := (PresheafOfModules.pushforward œÜ.val).map f.val }

/-- Given `M : SheafOfModules R` and `X : D`, this is the restriction of `M`
over the sheaf of rings `R.over X` on the category `Over X`. -/
noncomputable abbrev over (M : SheafOfModules.{v} R) (X : D) : SheafOfModules.{v} (R.over X) :=
  (pushforward.{v} (ùüô _)).obj M

section Functorial

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
  {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E]
  {J : GrothendieckTopology C} {K : GrothendieckTopology D} {L : GrothendieckTopology E}
  {F : C ‚•§ D} {G : D ‚•§ E}
  {T : Sheaf J RingCat.{u}} {S : Sheaf K RingCat.{u}} {R : Sheaf L RingCat.{u}}
  [Functor.IsContinuous.{u} F J K] [Functor.IsContinuous.{v} F J K]
  [Functor.IsContinuous.{u} G K L] [Functor.IsContinuous.{v} G K L]
  (œÜ : T ‚ü∂ (F.sheafPushforwardContinuous RingCat.{u} J K).obj S)
  (œà : S ‚ü∂ (G.sheafPushforwardContinuous RingCat.{u} K L).obj R)

/-- Pushforward along the identity is (isomorphic to) the identity. -/
noncomputable def pushforwardId : pushforward.{v} (F := ùü≠ _) (ùüô R) ‚âÖ ùü≠ _ := Iso.refl _

/-- Pushforward along equal morphisms of sheaves of rings is isomorphic. -/
noncomputable
def pushforwardCongr {œÜ œà : S ‚ü∂ (G.sheafPushforwardContinuous RingCat.{u} K L).obj R} (e : œÜ = œà) :
    pushforward.{v} œÜ ‚âÖ pushforward.{v} œà :=
  NatIso.ofComponents (fun X ‚Ü¶ (SheafOfModules.fullyFaithfulForget _).preimageIso
    (PresheafOfModules.isoMk (fun U ‚Ü¶ (ModuleCat.restrictScalarsCongr (by subst e; rfl)).app _)
      fun _ _ _ ‚Ü¶ by subst e; rfl)) fun _ ‚Ü¶ by subst e; rfl

@[simp] lemma pushforwardCongr_symm
    {œÜ œà : S ‚ü∂ (G.sheafPushforwardContinuous RingCat.{u} K L).obj R} (e : œÜ = œà) :
  (pushforwardCongr e).symm = pushforwardCongr e.symm := rfl

@[simp] lemma pushforwardCongr_hom_app_val_app
    {œÜ œà : S ‚ü∂ (G.sheafPushforwardContinuous RingCat.{u} K L).obj R} (e : œÜ = œà) (M U x) :
  ((pushforwardCongr e).hom.app M).val.app U x = x := rfl

/-- Composition of pushforwards is (isomorphic to) the pushforward along the composition. -/
noncomputable def pushforwardComp :
    letI := CategoryTheory.Functor.isContinuous_comp.{u} F G J K L
    letI := CategoryTheory.Functor.isContinuous_comp.{v} F G J K L
    pushforward.{v} œà ‚ãô pushforward.{v} œÜ ‚âÖ
      pushforward.{v} (F := F ‚ãô G) (œÜ ‚â´ (F.sheafPushforwardContinuous RingCat.{u} J K).map œà) :=
  NatIso.ofComponents (fun X ‚Ü¶ (SheafOfModules.fullyFaithfulForget _).preimageIso <|
    (PresheafOfModules.pushforwardComp œÜ.val œà.val).app X.val) fun f ‚Ü¶ SheafOfModules.hom_ext
      ((PresheafOfModules.pushforwardComp œÜ.val œà.val).hom.naturality f.val)

-- Not a simp because the type of the LHS is dsimp-able
lemma pushforwardComp_hom_app_val_app (M U x) :
  ((pushforwardComp œÜ œà).hom.app M).val.app U x = x := rfl

-- Not a simp because the type of the LHS is dsimp-able
lemma pushforwardComp_inv_app_val_app (M U x) :
  ((pushforwardComp œÜ œà).inv.app M).val.app U x = x := rfl

end Functorial

section NatTrans

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
  {J : GrothendieckTopology C} {K : GrothendieckTopology D}
  {F G H : C ‚•§ D} {T : Sheaf J RingCat.{u}} {S : Sheaf K RingCat.{u}}
  [Functor.IsContinuous.{u} F J K] [Functor.IsContinuous.{v} F J K]
  [Functor.IsContinuous.{u} G J K] [Functor.IsContinuous.{v} G J K]
  [Functor.IsContinuous.{u} H J K] [Functor.IsContinuous.{v} H J K]
  (œÜ : T ‚ü∂ (G.sheafPushforwardContinuous RingCat.{u} J K).obj S)

/-- A natural transformation gives a natural transformation between the pushforward functors. -/
noncomputable
def pushforwardNatTrans (Œ± : F ‚ü∂ G) :
    pushforward.{v} œÜ ‚ü∂
      pushforward.{v} (œÜ ‚â´ (Functor.sheafPushforwardContinuousNatTrans Œ± _ _ _).app S) where
  app X :=
  { val.app U := (ModuleCat.restrictScalars (œÜ.val.app U).hom).map (X.val.map (Œ±.app U.unop).op)
    val.naturality {U V} i := by
      ext x
      dsimp
      change (X.val.presheaf.map (G.map i.unop).op ‚â´ X.val.presheaf.map (Œ±.app V.unop).op) _ =
        (X.val.presheaf.map (Œ±.app U.unop).op ‚â´ X.val.presheaf.map (F.map i.unop).op) _
      simp only [‚Üê CategoryTheory.Functor.map_comp, ‚Üê op_comp, Œ±.naturality] }
  naturality {X Y} f := by
    ext U x
    exact congr($(f.val.naturality (Œ±.app U.unop).op) x).symm

@[simp] lemma pushforwardNatTrans_app_val_app (Œ± : F ‚ü∂ G) (M U x) :
    ((pushforwardNatTrans œÜ Œ±).app M).val.app U x = M.val.map (Œ±.app U.unop).op x := rfl

@[simp]
lemma pushforwardNatTrans_id :
    pushforwardNatTrans œÜ (ùüô G) = (pushforwardCongr (by cat_disch)).hom := by cat_disch

@[simp]
lemma pushforwardNatTrans_comp (Œ± : F ‚ü∂ G) (Œ≤ : G ‚ü∂ H)
    (œÜ : T ‚ü∂ (H.sheafPushforwardContinuous RingCat.{u} J K).obj S) :
    pushforwardNatTrans œÜ (Œ± ‚â´ Œ≤) = pushforwardNatTrans œÜ Œ≤ ‚â´ pushforwardNatTrans _ Œ± ‚â´
      (pushforwardCongr (by cat_disch)).hom := by cat_disch

@[simp]
lemma pushforwardNatTrans_app_val_app_apply (Œ± : F ‚ü∂ G) (X U x) :
    ((pushforwardNatTrans œÜ Œ±).app X).val.app U x = X.val.map (Œ±.app U.unop).op x := rfl

/-- A natural isomorphism gives a natural isomorphism between the pushforward functors. -/
@[simps hom inv]
noncomputable def pushforwardNatIso (Œ± : F ‚âÖ G) :
    pushforward.{v} œÜ ‚âÖ
      pushforward.{v} (œÜ ‚â´ (Functor.sheafPushforwardContinuousNatTrans Œ±.hom _ _ _).app S) where
  hom := pushforwardNatTrans _ Œ±.hom
  inv := pushforwardNatTrans _ Œ±.inv ‚â´
    (pushforwardCongr (by ext : 3; simp [‚Üê Functor.map_comp, ‚Üê op_comp])).hom
  hom_inv_id := by
    ext X U x
    suffices X.val.presheaf.map (Œ±.hom.app U.unop).op ‚â´
      X.val.presheaf.map (Œ±.inv.app U.unop).op = ùüô _ from congr($this x)
    simp only [‚Üê Functor.map_comp, ‚Üê op_comp,
      Iso.inv_hom_id_app, op_id, CategoryTheory.Functor.map_id]
  inv_hom_id := by
    ext X U x
    suffices X.val.presheaf.map (Œ±.inv.app U.unop).op ‚â´
      X.val.presheaf.map (Œ±.hom.app U.unop).op = ùüô _ from congr($this x)
    simp only [‚Üê Functor.map_comp, ‚Üê op_comp,
      Iso.hom_inv_id_app, op_id, CategoryTheory.Functor.map_id]

end NatTrans

section Adjunction

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
  {J : GrothendieckTopology C} {K : GrothendieckTopology D} {F : C ‚•§ D} {G : D ‚•§ C}
  {S : Sheaf J RingCat.{u}} {R : Sheaf K RingCat.{u}}
  [Functor.IsContinuous.{u} F J K] [Functor.IsContinuous.{v} F J K]
  [Functor.IsContinuous.{u} G K J] [Functor.IsContinuous.{v} G K J]
  (adj : F ‚ä£ G)
  (œÜ : S ‚ü∂ (F.sheafPushforwardContinuous RingCat.{u} J K).obj R)
  (œà : R ‚ü∂ (G.sheafPushforwardContinuous RingCat.{u} K J).obj S)
  (H‚ÇÅ : Functor.whiskerRight (NatTrans.op adj.counit) R.val = œà.val ‚â´ G.op.whiskerLeft œÜ.val)
  (H‚ÇÇ : œÜ.val ‚â´ F.op.whiskerLeft œà.val ‚â´
    Functor.whiskerRight (NatTrans.op adj.unit) S.val = ùüô S.val)

/-- If `F ‚ä£ G`, then the pushforward along `F` and `G` are also adjoint. -/
noncomputable
def pushforwardPushforwardAdj : pushforward.{v} œÜ ‚ä£ pushforward.{v} œà where
  unit :=
    letI := CategoryTheory.Functor.isContinuous_comp.{v} G F K J K
    letI := CategoryTheory.Functor.isContinuous_comp.{u} G F K J K
    pushforwardId.inv ‚â´ pushforwardNatTrans (ùüô _) adj.counit ‚â´
      (pushforwardCongr (by ext1; simpa)).hom ‚â´ (pushforwardComp _ _).inv
  counit :=
    letI := CategoryTheory.Functor.isContinuous_comp.{v} F G J K J
    letI := CategoryTheory.Functor.isContinuous_comp.{u} F G J K J
    (pushforwardComp _ _).hom ‚â´ pushforwardNatTrans _ adj.unit ‚â´
      (pushforwardCongr (by ext1; simpa)).hom ‚â´ pushforwardId.hom
  left_triangle_components X := by
    ext U x
    change (X.val.presheaf.map (adj.counit.app (F.obj U.unop)).op ‚â´
      X.val.presheaf.map (F.map (adj.unit.app U.unop)).op) _ = _
    rw [‚Üê Functor.map_comp, ‚Üê op_comp, adj.left_triangle_components]
    simp
  right_triangle_components X := by
    ext U x
    change (X.val.presheaf.map (G.map (adj.counit.app U.unop)).op ‚â´
      X.val.presheaf.map (adj.unit.app (G.obj U.unop)).op) _ = _
    rw [‚Üê Functor.map_comp, ‚Üê op_comp, adj.right_triangle_components]
    simp

-- Not a simp because the type of the LHS is dsimp-able
lemma pushforwardPushforwardAdj_unit_app_val_app (M U x) :
    ((pushforwardPushforwardAdj adj œÜ œà H‚ÇÅ H‚ÇÇ).unit.app M).val.app U x =
      M.val.map (adj.counit.app U.unop).op x := rfl

-- Not a simp because the type of the LHS is dsimp-able
lemma pushforwardPushforwardAdj_counit_app_val_app (M U x) :
    ((pushforwardPushforwardAdj adj œÜ œà H‚ÇÅ H‚ÇÇ).counit.app M).val.app U x =
      M.val.map (adj.unit.app U.unop).op x := rfl

end Adjunction

end SheafOfModules
