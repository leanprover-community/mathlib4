/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
module

public import Mathlib.Algebra.Category.ModuleCat.Presheaf.Pushforward
public import Mathlib.Algebra.Category.ModuleCat.Sheaf
public import Mathlib.CategoryTheory.Sites.Over

/-!
# Pushforward of sheaves of modules

Assume that categories `C` and `D` are equipped with Grothendieck topologies, and
that `F : C â¥¤ D` is a continuous functor.
Then, if `Ï† : S âŸ¶ (F.sheafPushforwardContinuous RingCat.{u} J K).obj R` is
a morphism of sheaves of rings, we construct the pushforward functor
`pushforward Ï† : SheafOfModules.{v} R â¥¤ SheafOfModules.{v} S`, and
we show that they interact with the composition of morphisms similarly as pseudofunctors.

-/

@[expose] public section

universe v' u' v vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ uâ‚ uâ‚‚ uâ‚ƒ uâ‚„ u

open CategoryTheory Functor

namespace SheafOfModules

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D]
  {D' : Type uâ‚ƒ} [Category.{vâ‚ƒ} D'] {D'' : Type uâ‚„} [Category.{vâ‚„} D'']
  {J : GrothendieckTopology C} {K : GrothendieckTopology D} {F : C â¥¤ D}
  {S : Sheaf J RingCat.{u}} {R : Sheaf K RingCat.{u}}
  [Functor.IsContinuous.{u} F J K] [Functor.IsContinuous.{v} F J K]
  (Ï† : S âŸ¶ (F.sheafPushforwardContinuous RingCat.{u} J K).obj R)

/-- The pushforward of sheaves of modules that is induced by a continuous functor `F`
and a morphism of sheaves of rings `Ï† : S âŸ¶ (F.sheafPushforwardContinuous RingCat J K).obj R`. -/
@[simps map_val, simps -isSimp obj_val]
noncomputable def pushforward : SheafOfModules.{v} R â¥¤ SheafOfModules.{v} S where
  obj M :=
    { val := (PresheafOfModules.pushforward Ï†.val).obj M.val
      isSheaf := ((F.sheafPushforwardContinuous _ J K).obj âŸ¨_, M.isSheafâŸ©).cond }
  map f :=
    { val := (PresheafOfModules.pushforward Ï†.val).map f.val }

/-- Given `M : SheafOfModules R` and `X : D`, this is the restriction of `M`
over the sheaf of rings `R.over X` on the category `Over X`. -/
noncomputable abbrev over (M : SheafOfModules.{v} R) (X : D) : SheafOfModules.{v} (R.over X) :=
  (pushforward.{v} (ğŸ™ _)).obj M

section

variable (R) in
/-- The pushforward functor by the identity morphism identifies to
the identify functor of the category of sheaves of modules. -/
noncomputable def pushforwardId :
    pushforward.{v} (S := R) (F := ğŸ­ _) (ğŸ™ R) â‰… ğŸ­ _ :=
  Iso.refl _

/-- Pushforwards along equal morphisms of sheaves of rings are isomorphic. -/
noncomputable
def pushforwardCongr {Ï† Ïˆ : S âŸ¶ (F.sheafPushforwardContinuous RingCat.{u} J K).obj R} (e : Ï† = Ïˆ) :
    pushforward.{v} Ï† â‰… pushforward.{v} Ïˆ :=
  NatIso.ofComponents (fun X â†¦ (SheafOfModules.fullyFaithfulForget _).preimageIso
    (PresheafOfModules.isoMk (fun U â†¦ (ModuleCat.restrictScalarsCongr (by subst e; rfl)).app _)
      fun _ _ _ â†¦ by subst e; rfl)) fun _ â†¦ by subst e; rfl

@[simp] lemma pushforwardCongr_symm
    {Ï† Ïˆ : S âŸ¶ (F.sheafPushforwardContinuous RingCat.{u} J K).obj R} (e : Ï† = Ïˆ) :
  (pushforwardCongr e).symm = pushforwardCongr e.symm := rfl

@[simp] lemma pushforwardCongr_hom_app_val_app
    {Ï† Ïˆ : S âŸ¶ (F.sheafPushforwardContinuous RingCat.{u} J K).obj R} (e : Ï† = Ïˆ) (M U x) :
  ((pushforwardCongr e).hom.app M).val.app U x = x := rfl

@[simp] lemma pushforwardCongr_inv_app_val_app
    {Ï† Ïˆ : S âŸ¶ (F.sheafPushforwardContinuous RingCat.{u} J K).obj R} (e : Ï† = Ïˆ) (M U x) :
  ((pushforwardCongr e).inv.app M).val.app U x = x := rfl

section

variable {K' : GrothendieckTopology D'} {K'' : GrothendieckTopology D''}
  {G : D â¥¤ D'} {R' : Sheaf K' RingCat.{u}}
  [Functor.IsContinuous.{u} G K K'] [Functor.IsContinuous.{v} G K K']
  [Functor.IsContinuous.{u} (F â‹™ G) J K'] [Functor.IsContinuous.{v} (F â‹™ G) J K']
  (Ïˆ : R âŸ¶ (G.sheafPushforwardContinuous RingCat.{u} K K').obj R')

/-- The composition of two pushforward functors on categories of sheaves of modules
identify to the pushforward for the composition. -/
noncomputable def pushforwardComp :
    pushforward.{v} Ïˆ â‹™ pushforward.{v} Ï† â‰…
      pushforward.{v} (F := F â‹™ G) (Ï† â‰« (F.sheafPushforwardContinuous RingCat.{u} J K).map Ïˆ) :=
  Iso.refl _

-- Not a simp because the type of the LHS is dsimp-able
lemma pushforwardComp_hom_app_val_app (M U x) :
  ((pushforwardComp Ï† Ïˆ).hom.app M).val.app U x = x := rfl

-- Not a simp because the type of the LHS is dsimp-able
lemma pushforwardComp_inv_app_val_app (M U x) :
  ((pushforwardComp Ï† Ïˆ).inv.app M).val.app U x = x := rfl

variable {G' : D' â¥¤ D''} {R'' : Sheaf K'' RingCat.{u}}
  [Functor.IsContinuous.{u} G' K' K''] [Functor.IsContinuous.{v} G' K' K'']
  [Functor.IsContinuous.{u} (G â‹™ G') K K'']
  [Functor.IsContinuous.{v} (G â‹™ G') K K'']
  [Functor.IsContinuous.{u} ((F â‹™ G) â‹™ G') J K'']
  [Functor.IsContinuous.{v} ((F â‹™ G) â‹™ G') J K'']
  [Functor.IsContinuous.{u} (F â‹™ G â‹™ G') J K'']
  [Functor.IsContinuous.{v} (F â‹™ G â‹™ G') J K'']
  (Ïˆ' : R' âŸ¶ (G'.sheafPushforwardContinuous RingCat.{u} K' K'').obj R'')

lemma pushforward_assoc :
    (pushforward Ïˆ').isoWhiskerLeft (pushforwardComp Ï† Ïˆ) â‰ªâ‰«
      pushforwardComp (F := F â‹™ G)
        (Ï† â‰« (F.sheafPushforwardContinuous RingCat.{u} J K).map Ïˆ) Ïˆ' =
    ((pushforward Ïˆ').associator (pushforward Ïˆ) (pushforward Ï†)).symm â‰ªâ‰«
      isoWhiskerRight (pushforwardComp Ïˆ Ïˆ') (pushforward Ï†) â‰ªâ‰«
      pushforwardComp (G := G â‹™ G') Ï† (Ïˆ â‰«
        (G.sheafPushforwardContinuous RingCat.{u} K K').map Ïˆ') := by ext; rfl

end

lemma pushforward_comp_id :
    pushforwardComp.{v} (F := ğŸ­ C) (ğŸ™ S) Ï† =
      isoWhiskerLeft (pushforward.{v} Ï†) (pushforwardId S) â‰ªâ‰« rightUnitor _ := by ext; rfl

lemma pushforward_id_comp :
    pushforwardComp.{v} (G := ğŸ­ _) Ï† (ğŸ™ R) =
      isoWhiskerRight (pushforwardId R) (pushforward.{v} Ï†) â‰ªâ‰« leftUnitor _ := by ext; rfl

end

section NatTrans

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D]
  {J : GrothendieckTopology C} {K : GrothendieckTopology D}
  {F G H : C â¥¤ D} {T : Sheaf J RingCat.{u}} {S : Sheaf K RingCat.{u}}
  [Functor.IsContinuous.{u} F J K] [Functor.IsContinuous.{v} F J K]
  [Functor.IsContinuous.{u} G J K] [Functor.IsContinuous.{v} G J K]
  [Functor.IsContinuous.{u} H J K] [Functor.IsContinuous.{v} H J K]
  (Ï† : T âŸ¶ (G.sheafPushforwardContinuous RingCat.{u} J K).obj S)

/-- A natural transformation gives a natural transformation between the pushforward functors. -/
noncomputable
def pushforwardNatTrans (Î± : F âŸ¶ G) :
    pushforward.{v} Ï† âŸ¶
      pushforward.{v} (Ï† â‰« (Functor.sheafPushforwardContinuousNatTrans Î± _ _ _).app S) where
  app X :=
  { val.app U := (ModuleCat.restrictScalars (Ï†.val.app U).hom).map (X.val.map (Î±.app U.unop).op)
    val.naturality {U V} i := by
      ext x
      dsimp
      change (X.val.presheaf.map (G.map i.unop).op â‰« X.val.presheaf.map (Î±.app V.unop).op) _ =
        (X.val.presheaf.map (Î±.app U.unop).op â‰« X.val.presheaf.map (F.map i.unop).op) _
      simp only [â† CategoryTheory.Functor.map_comp, â† op_comp, Î±.naturality] }
  naturality {X Y} f := by
    ext U x
    exact congr($(f.val.naturality (Î±.app U.unop).op) x).symm

@[simp] lemma pushforwardNatTrans_app_val_app (Î± : F âŸ¶ G) (M U x) :
    ((pushforwardNatTrans Ï† Î±).app M).val.app U x = M.val.map (Î±.app U.unop).op x := rfl

@[simp]
lemma pushforwardNatTrans_id :
    pushforwardNatTrans Ï† (ğŸ™ G) = (pushforwardCongr (by cat_disch)).hom := by cat_disch

@[simp]
lemma pushforwardNatTrans_comp (Î± : F âŸ¶ G) (Î² : G âŸ¶ H)
    (Ï† : T âŸ¶ (H.sheafPushforwardContinuous RingCat.{u} J K).obj S) :
    pushforwardNatTrans Ï† (Î± â‰« Î²) = pushforwardNatTrans Ï† Î² â‰« pushforwardNatTrans _ Î± â‰«
      (pushforwardCongr (by cat_disch)).hom := by cat_disch

@[simp]
lemma pushforwardNatTrans_app_val_app_apply (Î± : F âŸ¶ G) (X U x) :
    ((pushforwardNatTrans Ï† Î±).app X).val.app U x = X.val.map (Î±.app U.unop).op x := rfl

/-- A natural isomorphism gives a natural isomorphism between the pushforward functors. -/
@[simps]
noncomputable def pushforwardNatIso (Î± : F â‰… G) :
    pushforward.{v} Ï† â‰…
      pushforward.{v} (Ï† â‰« (Functor.sheafPushforwardContinuousNatTrans Î±.hom _ _ _).app S) where
  hom := pushforwardNatTrans _ Î±.hom
  inv := pushforwardNatTrans _ Î±.inv â‰«
    (pushforwardCongr (by ext : 3; simp [â† Functor.map_comp, â† op_comp])).hom
  hom_inv_id := by
    ext X U x
    suffices X.val.presheaf.map (Î±.hom.app U.unop).op â‰«
      X.val.presheaf.map (Î±.inv.app U.unop).op = ğŸ™ _ from congr($this x)
    simp only [â† Functor.map_comp, â† op_comp,
      Iso.inv_hom_id_app, op_id, CategoryTheory.Functor.map_id]
  inv_hom_id := by
    ext X U x
    suffices X.val.presheaf.map (Î±.inv.app U.unop).op â‰«
      X.val.presheaf.map (Î±.hom.app U.unop).op = ğŸ™ _ from congr($this x)
    simp only [â† Functor.map_comp, â† op_comp,
      Iso.hom_inv_id_app, op_id, CategoryTheory.Functor.map_id]

end NatTrans

section Adjunction

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D]
  {J : GrothendieckTopology C} {K : GrothendieckTopology D} {F : C â¥¤ D} {G : D â¥¤ C}
  {S : Sheaf J RingCat.{u}} {R : Sheaf K RingCat.{u}}
  [Functor.IsContinuous.{u} F J K] [Functor.IsContinuous.{v} F J K]
  [Functor.IsContinuous.{u} G K J] [Functor.IsContinuous.{v} G K J]
  (adj : F âŠ£ G)
  (Ï† : S âŸ¶ (F.sheafPushforwardContinuous RingCat.{u} J K).obj R)
  (Ïˆ : R âŸ¶ (G.sheafPushforwardContinuous RingCat.{u} K J).obj S)
  (Hâ‚ : Functor.whiskerRight (NatTrans.op adj.counit) R.val = Ïˆ.val â‰« G.op.whiskerLeft Ï†.val)
  (Hâ‚‚ : Ï†.val â‰« F.op.whiskerLeft Ïˆ.val â‰«
    Functor.whiskerRight (NatTrans.op adj.unit) S.val = ğŸ™ S.val)

/-- If `F âŠ£ G`, then the pushforwards along `F` and `G` are also adjoint. -/
noncomputable
def pushforwardPushforwardAdj : pushforward.{v} Ï† âŠ£ pushforward.{v} Ïˆ where
  unit :=
    letI := CategoryTheory.Functor.isContinuous_comp.{v} G F K J K
    letI := CategoryTheory.Functor.isContinuous_comp.{u} G F K J K
    (pushforwardId _).inv â‰« pushforwardNatTrans (ğŸ™ _) adj.counit â‰«
      (pushforwardCongr (by ext1; simpa)).hom â‰« (pushforwardComp _ _).inv
  counit :=
    letI := CategoryTheory.Functor.isContinuous_comp.{v} F G J K J
    letI := CategoryTheory.Functor.isContinuous_comp.{u} F G J K J
    (pushforwardComp _ _).hom â‰« pushforwardNatTrans _ adj.unit â‰«
      (pushforwardCongr (by ext1; simpa)).hom â‰« (pushforwardId _).hom
  left_triangle_components X := by
    ext U x
    change (X.val.presheaf.map (adj.counit.app (F.obj U.unop)).op â‰«
      X.val.presheaf.map (F.map (adj.unit.app U.unop)).op) _ = _
    rw [â† Functor.map_comp, â† op_comp, adj.left_triangle_components]
    simp
  right_triangle_components X := by
    ext U x
    change (X.val.presheaf.map (G.map (adj.counit.app U.unop)).op â‰«
      X.val.presheaf.map (adj.unit.app (G.obj U.unop)).op) _ = _
    rw [â† Functor.map_comp, â† op_comp, adj.right_triangle_components]
    simp

-- Not a simp because the type of the LHS is dsimp-able
lemma pushforwardPushforwardAdj_unit_app_val_app (M U x) :
    ((pushforwardPushforwardAdj adj Ï† Ïˆ Hâ‚ Hâ‚‚).unit.app M).val.app U x =
      M.val.map (adj.counit.app U.unop).op x := rfl

-- Not a simp because the type of the LHS is dsimp-able
lemma pushforwardPushforwardAdj_counit_app_val_app (M U x) :
    ((pushforwardPushforwardAdj adj Ï† Ïˆ Hâ‚ Hâ‚‚).counit.app M).val.app U x =
      M.val.map (adj.unit.app U.unop).op x := rfl

end Adjunction

end SheafOfModules
