/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Category.ModuleCat.Presheaf.Pullback
import Mathlib.Algebra.Category.ModuleCat.Presheaf.Sheafification
import Mathlib.Algebra.Category.ModuleCat.Sheaf.PushforwardContinuous

/-!
# Pullback of sheaves of modules

Let `S` and `R` be sheaves of rings over sites `(C, J)` and `(D, K)` respectively.
Let `F : C ‚•§ D` be a continuous functor between these sites, and
let `œÜ : S ‚ü∂ (F.sheafPushforwardContinuous RingCat.{u} J K).obj R` be a morphism
of sheaves of rings.

In this file, we define the pullback functor for sheaves of modules
`pullback.{v} œÜ : SheafOfModules.{v} S ‚•§ SheafOfModules.{v} R`
that is left adjoint to `pushforward.{v} œÜ`. We show that it exists
under suitable assumptions, and prove that the pullback of (pre)sheaves of
modules commutes with the sheafification.

-/

universe v v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ u

open CategoryTheory

namespace SheafOfModules

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
  {J : GrothendieckTopology C} {K : GrothendieckTopology D} {F : C ‚•§ D}
  {S : Sheaf J RingCat.{u}} {R : Sheaf K RingCat.{u}}
  [Functor.IsContinuous.{u} F J K] [Functor.IsContinuous.{v} F J K]
  (œÜ : S ‚ü∂ (F.sheafPushforwardContinuous RingCat.{u} J K).obj R)

section

variable [(pushforward.{v} œÜ).IsRightAdjoint]

/-- The pullback functor `SheafOfModules S ‚•§ SheafOfModules R` induced by
a morphism of sheaves of rings `S ‚ü∂ (F.sheafPushforwardContinuous RingCat.{u} J K).obj R`,
defined as the left adjoint functor to the pushforward, when it exists. -/
noncomputable def pullback : SheafOfModules.{v} S ‚•§ SheafOfModules.{v} R :=
  (pushforward.{v} œÜ).leftAdjoint

/-- Given a continuous functor between sites `F`, and a morphism of sheaves of rings
`S ‚ü∂ (F.sheafPushforwardContinuous RingCat.{u} J K).obj R`, this is the adjunction
between the corresponding pullback and pushforward functors on the categories
of sheaves of modules. -/
noncomputable def pullbackPushforwardAdjunction : pullback.{v} œÜ ‚ä£ pushforward.{v} œÜ :=
  Adjunction.ofIsRightAdjoint (pushforward œÜ)

end

section

variable [(PresheafOfModules.pushforward.{v} œÜ.val).IsRightAdjoint]
  [HasWeakSheafify K AddCommGrpCat.{v}] [K.WEqualsLocallyBijective AddCommGrpCat.{v}]

namespace PullbackConstruction

/-- Construction of a left adjoint to the functor `pushforward.{v} œÜ` by using the
pullback of presheaves of modules and the sheafification. -/
noncomputable def adjunction :
    (forget S ‚ãô PresheafOfModules.pullback.{v} œÜ.val ‚ãô
      PresheafOfModules.sheafification (ùüô R.val)) ‚ä£ pushforward.{v} œÜ :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun F G ‚Ü¶
        ((PresheafOfModules.sheafificationAdjunction (ùüô R.val)).homEquiv _ _).trans
            (((PresheafOfModules.pullbackPushforwardAdjunction œÜ.val).homEquiv F.val G.val).trans
              ((fullyFaithfulForget S).homEquiv (Y := (pushforward œÜ).obj G)).symm)
      homEquiv_naturality_left_symm := by
        intros
        dsimp [Functor.FullyFaithful.homEquiv]
        -- these erw seem difficult to remove
        erw [Adjunction.homEquiv_naturality_left_symm,
          Adjunction.homEquiv_naturality_left_symm]
        dsimp
        simp only [Functor.map_comp, Category.assoc]
      homEquiv_naturality_right := by
        tauto }

end PullbackConstruction

instance : (pushforward.{v} œÜ).IsRightAdjoint :=
  (PullbackConstruction.adjunction.{v} œÜ).isRightAdjoint

/-- The pullback functor on sheaves of modules can be described as a composition
of the forget functor to presheaves, the pullback on presheaves of modules, and
the sheafification functor. -/
noncomputable def pullbackIso :
    pullback.{v} œÜ ‚âÖ
      forget S ‚ãô PresheafOfModules.pullback.{v} œÜ.val ‚ãô
        PresheafOfModules.sheafification (ùüô R.val) :=
  Adjunction.leftAdjointUniq (pullbackPushforwardAdjunction œÜ)
    (PullbackConstruction.adjunction œÜ)

section

variable [HasWeakSheafify J AddCommGrpCat.{v}] [J.WEqualsLocallyBijective AddCommGrpCat.{v}]

/-- The pullback of (pre)sheaves of modules commutes with the sheafification. -/
noncomputable def sheafificationCompPullback :
    PresheafOfModules.sheafification (ùüô S.val) ‚ãô pullback.{v} œÜ ‚âÖ
      PresheafOfModules.pullback.{v} œÜ.val ‚ãô
        PresheafOfModules.sheafification (ùüô R.val) :=
  Adjunction.leftAdjointUniq
    ((PresheafOfModules.sheafificationAdjunction (ùüô S.val)).comp
      (pullbackPushforwardAdjunction œÜ))
    ((PresheafOfModules.pullbackPushforwardAdjunction œÜ.val).comp
      (PresheafOfModules.sheafificationAdjunction (ùüô R.val)))

end

end

end SheafOfModules
