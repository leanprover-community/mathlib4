/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Scott Morrison, Jakob von Raumer
-/
import Mathlib.CategoryTheory.Monoidal.Braided
import Mathlib.Algebra.Category.ModuleCat.Monoidal.Basic

#align_import algebra.category.Module.monoidal.symmetric from "leanprover-community/mathlib"@"74403a3b2551b0970855e14ef5e8fd0d6af1bfc2"

/-!
# The symmetric monoidal structure on `Module R`.
-/

suppress_compilation

universe v w x u

open CategoryTheory MonoidalCategory

namespace ModuleCat

variable {R : Type u} [CommRing R]

/-- (implementation) the braiding for R-modules -/
def braiding (M N : ModuleCat.{u} R) : M âŠ— N â‰… N âŠ— M :=
  LinearEquiv.toModuleIso (TensorProduct.comm R M N)

namespace MonoidalCategory

@[simp]
theorem braiding_naturality {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : ModuleCat.{u} R} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚) :
    (f âŠ— g) â‰« (Yâ‚.braiding Yâ‚‚).hom = (Xâ‚.braiding Xâ‚‚).hom â‰« (g âŠ— f) := by
  apply TensorProduct.ext'
  intro x y
  rfl

@[simp]
theorem hexagon_forward (X Y Z : ModuleCat.{u} R) :
    (Î±_ X Y Z).hom â‰« (braiding X _).hom â‰« (Î±_ Y Z X).hom =
      ((braiding X Y).hom âŠ— ğŸ™ Z) â‰« (Î±_ Y X Z).hom â‰« (ğŸ™ Y âŠ— (braiding X Z).hom) := by
  apply TensorProduct.ext_threefold
  intro x y z
  rfl

@[simp]
theorem hexagon_reverse (X Y Z : ModuleCat.{u} R) :
    (Î±_ X Y Z).inv â‰« (braiding _ Z).hom â‰« (Î±_ Z X Y).inv =
      (ğŸ™ X âŠ— (Y.braiding Z).hom) â‰« (Î±_ X Z Y).inv â‰« ((X.braiding Z).hom âŠ— ğŸ™ Y) := by
  apply (cancel_epi (Î±_ X Y Z).hom).1
  apply TensorProduct.ext_threefold
  intro x y z
  rfl

attribute [local ext] TensorProduct.ext

/-- The symmetric monoidal structure on `Module R`. -/
instance symmetricCategory : SymmetricCategory (ModuleCat.{u} R) where
  braiding := braiding
  braiding_naturality f g := braiding_naturality f g
  hexagon_forward := hexagon_forward
  hexagon_reverse := hexagon_reverse
  -- porting note: this proof was automatic in Lean3
  -- now `aesop` is applying `ModuleCat.ext` in favour of `TensorProduct.ext`.
  symmetry _ _ := by
    apply TensorProduct.ext'
    aesop_cat

@[simp]
theorem braiding_hom_apply {M N : ModuleCat.{u} R} (m : M) (n : N) :
    ((Î²_ M N).hom : M âŠ— N âŸ¶ N âŠ— M) (m âŠ—â‚œ n) = n âŠ—â‚œ m :=
  rfl

@[simp]
theorem braiding_inv_apply {M N : ModuleCat.{u} R} (m : M) (n : N) :
    ((Î²_ M N).inv : N âŠ— M âŸ¶ M âŠ— N) (n âŠ—â‚œ m) = m âŠ—â‚œ n :=
  rfl

end MonoidalCategory

end ModuleCat
