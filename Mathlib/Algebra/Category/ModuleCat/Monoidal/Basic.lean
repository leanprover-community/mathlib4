/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Kim Morrison, Jakob von Raumer
-/
import Mathlib.Algebra.Category.ModuleCat.Basic
import Mathlib.LinearAlgebra.TensorProduct.Associator
import Mathlib.CategoryTheory.Monoidal.Linear

/-!
# The monoidal category structure on R-modules

Mostly this uses existing machinery in `LinearAlgebra.TensorProduct`.
We just need to provide a few small missing pieces to build the
`MonoidalCategory` instance.
The `SymmetricCategory` instance is in `Algebra.Category.ModuleCat.Monoidal.Symmetric`
to reduce imports.

Note the universe level of the modules must be at least the universe level of the ring,
so that we have a monoidal unit.
For now, we simplify by insisting both universe levels are the same.

We construct the monoidal closed structure on `ModuleCat R` in
`Algebra.Category.ModuleCat.Monoidal.Closed`.

If you're happy using the bundled `ModuleCat R`, it may be possible to mostly
use this as an interface and not need to interact much with the implementation details.
-/


suppress_compilation

universe v w x u

open CategoryTheory

namespace ModuleCat

variable {R : Type u} [CommRing R]

namespace MonoidalCategory

-- The definitions inside this namespace are essentially private.
-- After we build the `MonoidalCategory (Module R)` instance,
-- you should use that API.
open TensorProduct

attribute [local ext] TensorProduct.ext

/-- (implementation) tensor product of R-modules -/
def tensorObj (M N : ModuleCat R) : ModuleCat R :=
  ModuleCat.of R (M ‚äó[R] N)

/-- (implementation) tensor product of morphisms R-modules -/
def tensorHom {M N M' N' : ModuleCat R} (f : M ‚ü∂ N) (g : M' ‚ü∂ N') :
    tensorObj M M' ‚ü∂ tensorObj N N' :=
  ofHom <| TensorProduct.map f.hom g.hom

/-- (implementation) left whiskering for R-modules -/
def whiskerLeft (M : ModuleCat R) {N‚ÇÅ N‚ÇÇ : ModuleCat R} (f : N‚ÇÅ ‚ü∂ N‚ÇÇ) :
    tensorObj M N‚ÇÅ ‚ü∂ tensorObj M N‚ÇÇ :=
  ofHom <| f.hom.lTensor M

/-- (implementation) right whiskering for R-modules -/
def whiskerRight {M‚ÇÅ M‚ÇÇ : ModuleCat R} (f : M‚ÇÅ ‚ü∂ M‚ÇÇ) (N : ModuleCat R) :
    tensorObj M‚ÇÅ N ‚ü∂ tensorObj M‚ÇÇ N :=
  ofHom <| f.hom.rTensor N

theorem tensor_id (M N : ModuleCat R) : tensorHom (ùüô M) (ùüô N) = ùüô (ModuleCat.of R (M ‚äó N)) := by
  ext : 1
  -- Porting note (https://github.com/leanprover-community/mathlib4/pull/11041): even with high priority `ext` fails to find this.
  apply TensorProduct.ext
  rfl

theorem tensor_comp {X‚ÇÅ Y‚ÇÅ Z‚ÇÅ X‚ÇÇ Y‚ÇÇ Z‚ÇÇ : ModuleCat R} (f‚ÇÅ : X‚ÇÅ ‚ü∂ Y‚ÇÅ) (f‚ÇÇ : X‚ÇÇ ‚ü∂ Y‚ÇÇ) (g‚ÇÅ : Y‚ÇÅ ‚ü∂ Z‚ÇÅ)
    (g‚ÇÇ : Y‚ÇÇ ‚ü∂ Z‚ÇÇ) : tensorHom (f‚ÇÅ ‚â´ g‚ÇÅ) (f‚ÇÇ ‚â´ g‚ÇÇ) = tensorHom f‚ÇÅ f‚ÇÇ ‚â´ tensorHom g‚ÇÅ g‚ÇÇ := by
  ext : 1
  -- Porting note (https://github.com/leanprover-community/mathlib4/pull/11041): even with high priority `ext` fails to find this.
  apply TensorProduct.ext
  rfl

/-- (implementation) the associator for R-modules -/
def associator (M : ModuleCat.{v} R) (N : ModuleCat.{w} R) (K : ModuleCat.{x} R) :
    tensorObj (tensorObj M N) K ‚âÖ tensorObj M (tensorObj N K) :=
  (TensorProduct.assoc R M N K).toModuleIso

/-- (implementation) the left unitor for R-modules -/
def leftUnitor (M : ModuleCat.{u} R) : ModuleCat.of R (R ‚äó[R] M) ‚âÖ M :=
  (LinearEquiv.toModuleIso (TensorProduct.lid R M) : of R (R ‚äó M) ‚âÖ of R M).trans (ofSelfIso M)

/-- (implementation) the right unitor for R-modules -/
def rightUnitor (M : ModuleCat.{u} R) : ModuleCat.of R (M ‚äó[R] R) ‚âÖ M :=
  (LinearEquiv.toModuleIso (TensorProduct.rid R M) : of R (M ‚äó R) ‚âÖ of R M).trans (ofSelfIso M)

instance instMonoidalCategoryStruct : MonoidalCategoryStruct (ModuleCat.{u} R) where
  tensorObj := tensorObj
  whiskerLeft := whiskerLeft
  whiskerRight := whiskerRight
  tensorHom f g := ofHom <| TensorProduct.map f.hom g.hom
  tensorUnit := ModuleCat.of R R
  associator := associator
  leftUnitor := leftUnitor
  rightUnitor := rightUnitor

open scoped MonoidalCategory

theorem associator_naturality {X‚ÇÅ X‚ÇÇ X‚ÇÉ Y‚ÇÅ Y‚ÇÇ Y‚ÇÉ : ModuleCat R} (f‚ÇÅ : X‚ÇÅ ‚ü∂ Y‚ÇÅ) (f‚ÇÇ : X‚ÇÇ ‚ü∂ Y‚ÇÇ)
    (f‚ÇÉ : X‚ÇÉ ‚ü∂ Y‚ÇÉ) :
    tensorHom (tensorHom f‚ÇÅ f‚ÇÇ) f‚ÇÉ ‚â´ (associator Y‚ÇÅ Y‚ÇÇ Y‚ÇÉ).hom =
      (associator X‚ÇÅ X‚ÇÇ X‚ÇÉ).hom ‚â´ tensorHom f‚ÇÅ (tensorHom f‚ÇÇ f‚ÇÉ) := by
  ext : 1
  apply TensorProduct.ext_threefold
  intro x y z
  rfl

theorem pentagon (W X Y Z : ModuleCat R) :
    whiskerRight (associator W X Y).hom Z ‚â´
        (associator W (tensorObj X Y) Z).hom ‚â´ whiskerLeft W (associator X Y Z).hom =
      (associator (tensorObj W X) Y Z).hom ‚â´ (associator W X (tensorObj Y Z)).hom := by
  ext : 1
  apply TensorProduct.ext_fourfold
  intro w x y z
  rfl

theorem leftUnitor_naturality {M N : ModuleCat R} (f : M ‚ü∂ N) :
    tensorHom (ùüô (ModuleCat.of R R)) f ‚â´ (leftUnitor N).hom = (leftUnitor M).hom ‚â´ f := by
  ext : 1
  -- Porting note (https://github.com/leanprover-community/mathlib4/pull/11041): broken ext
  apply TensorProduct.ext
  ext x
  dsimp
  erw [TensorProduct.lid_tmul, TensorProduct.lid_tmul]
  rw [LinearMap.map_smul]
  rfl

theorem rightUnitor_naturality {M N : ModuleCat R} (f : M ‚ü∂ N) :
    tensorHom f (ùüô (ModuleCat.of R R)) ‚â´ (rightUnitor N).hom = (rightUnitor M).hom ‚â´ f := by
  ext : 1
  -- Porting note (https://github.com/leanprover-community/mathlib4/pull/11041): broken ext
  apply TensorProduct.ext
  ext x
  dsimp
  erw [TensorProduct.rid_tmul, TensorProduct.rid_tmul]
  rw [LinearMap.map_smul]
  rfl

theorem triangle (M N : ModuleCat.{u} R) :
    (associator M (ModuleCat.of R R) N).hom ‚â´ tensorHom (ùüô M) (leftUnitor N).hom =
      tensorHom (rightUnitor M).hom (ùüô N) := by
  ext : 1
  apply TensorProduct.ext_threefold
  intro x y z
  -- Porting note (https://github.com/leanprover-community/mathlib4/pull/10934): used to be dsimp [tensorHom, associator]
  change x ‚äó‚Çú[R] ((leftUnitor N).hom) (y ‚äó‚Çú[R] z) = ((rightUnitor M).hom) (x ‚äó‚Çú[R] y) ‚äó‚Çú[R] z
  erw [TensorProduct.lid_tmul, TensorProduct.rid_tmul]
  exact (TensorProduct.smul_tmul _ _ _).symm

end MonoidalCategory

open MonoidalCategory

instance monoidalCategory : MonoidalCategory (ModuleCat.{u} R) := MonoidalCategory.ofTensorHom
  (tensor_id := fun M N ‚Ü¶ tensor_id M N)
  (tensor_comp := fun f g h ‚Ü¶ MonoidalCategory.tensor_comp f g h)
  (associator_naturality := fun f g h ‚Ü¶ MonoidalCategory.associator_naturality f g h)
  (leftUnitor_naturality := fun f ‚Ü¶ MonoidalCategory.leftUnitor_naturality f)
  (rightUnitor_naturality := fun f ‚Ü¶ rightUnitor_naturality f)
  (pentagon := fun M N K L ‚Ü¶ pentagon M N K L)
  (triangle := fun M N ‚Ü¶ triangle M N)

/-- Remind ourselves that the monoidal unit, being just `R`, is still a commutative ring. -/
instance : CommRing ((ùüô_ (ModuleCat.{u} R) : ModuleCat.{u} R) : Type u) :=
  inferInstanceAs <| CommRing R

@[simp]
theorem carrier_tensorObj {M N : ModuleCat.{u} R} :
    (M ‚äó N).carrier = TensorProduct R M.carrier N.carrier :=
  rfl

@[simp]
theorem carrier_tensorUnit : (ùüô_ _ : ModuleCat.{u} R).carrier = R :=
  rfl

@[simp]
theorem hom_tensorHom {K L M N : ModuleCat.{u} R} (f : K ‚ü∂ L) (g : M ‚ü∂ N) :
    (f ‚äó g).hom = TensorProduct.map f.hom g.hom :=
  rfl

@[simp]
theorem hom_whiskerLeft (L : ModuleCat.{u} R) {M N : ModuleCat.{u} R} (f : M ‚ü∂ N) :
    (L ‚óÅ f).hom = f.hom.lTensor L :=
  rfl

@[simp]
theorem hom_whiskerRight {L M : ModuleCat.{u} R} (f : L ‚ü∂ M) (N : ModuleCat.{u} R) :
    (f ‚ñ∑ N).hom = f.hom.rTensor N :=
  rfl

@[simp]
theorem hom_hom_leftUnitor {M : ModuleCat.{u} R} :
    (Œª_ M).hom.hom = (TensorProduct.lid _ _).toLinearMap :=
  rfl

@[simp]
theorem hom_inv_leftUnitor {M : ModuleCat.{u} R} :
    (Œª_ M).inv.hom = (TensorProduct.lid _ _).symm.toLinearMap :=
  rfl

@[simp]
theorem hom_hom_rightUnitor {M : ModuleCat.{u} R} :
    (œÅ_ M).hom.hom = (TensorProduct.rid _ _).toLinearMap :=
  rfl

@[simp]
theorem hom_inv_rightUnitor {M : ModuleCat.{u} R} :
    (œÅ_ M).inv.hom = (TensorProduct.rid _ _).symm.toLinearMap :=
  rfl

@[simp]
theorem hom_hom_associator {M N K : ModuleCat.{u} R} :
    (Œ±_ M N K).hom.hom = (TensorProduct.assoc _ _ _ _).toLinearMap :=
  rfl

@[simp]
theorem hom_inv_associator {M N K : ModuleCat.{u} R} :
    (Œ±_ M N K).inv.hom = (TensorProduct.assoc _ _ _ _).symm.toLinearMap :=
  rfl

namespace MonoidalCategory

@[simp]
theorem tensorHom_tmul {K L M N : ModuleCat.{u} R} (f : K ‚ü∂ L) (g : M ‚ü∂ N) (k : K) (m : M) :
    (f ‚äó g) (k ‚äó‚Çú m) = f k ‚äó‚Çú g m := by
  rfl

@[simp]
theorem whiskerLeft_apply (L : ModuleCat.{u} R) {M N : ModuleCat.{u} R} (f : M ‚ü∂ N)
    (l : L) (m : M) :
    (L ‚óÅ f) (l ‚äó‚Çú m) = l ‚äó‚Çú f m :=
  rfl

@[simp]
theorem whiskerRight_apply {L M : ModuleCat.{u} R} (f : L ‚ü∂ M) (N : ModuleCat.{u} R)
    (l : L) (n : N) :
    (f ‚ñ∑ N) (l ‚äó‚Çú n) = f l ‚äó‚Çú n :=
  rfl

theorem leftUnitor_hom_apply {M : ModuleCat.{u} R} (r : R) (m : M) :
    ((Œª_ M).hom : ùüô_ (ModuleCat R) ‚äó M ‚ü∂ M) (r ‚äó‚Çú[R] m) = r ‚Ä¢ m :=
  TensorProduct.lid_tmul m r

theorem leftUnitor_inv_apply {M : ModuleCat.{u} R} (m : M) :
    ((Œª_ M).inv : M ‚ü∂ ùüô_ (ModuleCat.{u} R) ‚äó M) m = 1 ‚äó‚Çú[R] m :=
  TensorProduct.lid_symm_apply m

theorem rightUnitor_hom_apply {M : ModuleCat.{u} R} (m : M) (r : R) :
    ((œÅ_ M).hom : M ‚äó ùüô_ (ModuleCat R) ‚ü∂ M) (m ‚äó‚Çú r) = r ‚Ä¢ m :=
  TensorProduct.rid_tmul m r

theorem rightUnitor_inv_apply {M : ModuleCat.{u} R} (m : M) :
    ((œÅ_ M).inv : M ‚ü∂ M ‚äó ùüô_ (ModuleCat.{u} R)) m = m ‚äó‚Çú[R] 1 :=
  TensorProduct.rid_symm_apply m

@[simp]
theorem associator_hom_apply {M N K : ModuleCat.{u} R} (m : M) (n : N) (k : K) :
    ((Œ±_ M N K).hom : (M ‚äó N) ‚äó K ‚ü∂ M ‚äó N ‚äó K) (m ‚äó‚Çú n ‚äó‚Çú k) = m ‚äó‚Çú (n ‚äó‚Çú k) :=
  rfl

@[simp]
theorem associator_inv_apply {M N K : ModuleCat.{u} R} (m : M) (n : N) (k : K) :
    ((Œ±_ M N K).inv : M ‚äó N ‚äó K ‚ü∂ (M ‚äó N) ‚äó K) (m ‚äó‚Çú (n ‚äó‚Çú k)) = m ‚äó‚Çú n ‚äó‚Çú k :=
  rfl

variable {M‚ÇÅ M‚ÇÇ M‚ÇÉ M‚ÇÑ : ModuleCat.{u} R}

section

variable (f : M‚ÇÅ ‚Üí M‚ÇÇ ‚Üí M‚ÇÉ) (h‚ÇÅ : ‚àÄ m‚ÇÅ m‚ÇÇ n, f (m‚ÇÅ + m‚ÇÇ) n = f m‚ÇÅ n + f m‚ÇÇ n)
  (h‚ÇÇ : ‚àÄ (a : R) m n, f (a ‚Ä¢ m) n = a ‚Ä¢ f m n)
  (h‚ÇÉ : ‚àÄ m n‚ÇÅ n‚ÇÇ, f m (n‚ÇÅ + n‚ÇÇ) = f m n‚ÇÅ + f m n‚ÇÇ)
  (h‚ÇÑ : ‚àÄ (a : R) m n, f m (a ‚Ä¢ n) = a ‚Ä¢ f m n)

/-- Construct for morphisms from the tensor product of two objects in `ModuleCat`. -/
noncomputable def tensorLift : M‚ÇÅ ‚äó M‚ÇÇ ‚ü∂ M‚ÇÉ :=
  ofHom <| TensorProduct.lift (LinearMap.mk‚ÇÇ R f h‚ÇÅ h‚ÇÇ h‚ÇÉ h‚ÇÑ)

@[simp]
lemma tensorLift_tmul (m : M‚ÇÅ) (n : M‚ÇÇ) :
    tensorLift f h‚ÇÅ h‚ÇÇ h‚ÇÉ h‚ÇÑ (m ‚äó‚Çú n) = f m n := rfl

end

lemma tensor_ext {f g : M‚ÇÅ ‚äó M‚ÇÇ ‚ü∂ M‚ÇÉ} (h : ‚àÄ m n, f.hom (m ‚äó‚Çú n) = g.hom (m ‚äó‚Çú n)) :
    f = g :=
  hom_ext <| TensorProduct.ext (by ext; apply h)

/-- Extensionality lemma for morphisms from a module of the form `(M‚ÇÅ ‚äó M‚ÇÇ) ‚äó M‚ÇÉ`. -/
lemma tensor_ext‚ÇÉ' {f g : (M‚ÇÅ ‚äó M‚ÇÇ) ‚äó M‚ÇÉ ‚ü∂ M‚ÇÑ}
    (h : ‚àÄ m‚ÇÅ m‚ÇÇ m‚ÇÉ, f (m‚ÇÅ ‚äó‚Çú m‚ÇÇ ‚äó‚Çú m‚ÇÉ) = g (m‚ÇÅ ‚äó‚Çú m‚ÇÇ ‚äó‚Çú m‚ÇÉ)) :
    f = g :=
  hom_ext <| TensorProduct.ext_threefold h

/-- Extensionality lemma for morphisms from a module of the form `M‚ÇÅ ‚äó (M‚ÇÇ ‚äó M‚ÇÉ)`. -/
lemma tensor_ext‚ÇÉ {f g : M‚ÇÅ ‚äó (M‚ÇÇ ‚äó M‚ÇÉ) ‚ü∂ M‚ÇÑ}
    (h : ‚àÄ m‚ÇÅ m‚ÇÇ m‚ÇÉ, f (m‚ÇÅ ‚äó‚Çú (m‚ÇÇ ‚äó‚Çú m‚ÇÉ)) = g (m‚ÇÅ ‚äó‚Çú (m‚ÇÇ ‚äó‚Çú m‚ÇÉ))) :
    f = g := by
  rw [‚Üê cancel_epi (Œ±_ _ _ _).hom]
  exact tensor_ext‚ÇÉ' h

end MonoidalCategory

open Opposite

instance : MonoidalPreadditive (ModuleCat.{u} R) where
  whiskerLeft_zero {_ _ _} := by ext : 1; simp
  zero_whiskerRight {_ _ _} := by ext : 1; simp
  whiskerLeft_add _ _ := by ext : 1; simp
  add_whiskerRight _ _ := by ext : 1; simp

instance : MonoidalLinear R (ModuleCat.{u} R) where
  whiskerLeft_smul _ _ _ _ _ := by ext : 1; simp
  smul_whiskerRight _ _ _ _ _ := by ext : 1; simp

@[simp] lemma ofHom‚ÇÇ_compr‚ÇÇ {M N P Q : ModuleCat.{u} R} (f : M ‚Üí‚Çó[R] N ‚Üí‚Çó[R] P) (g : P ‚Üí‚Çó[R] Q):
    ofHom‚ÇÇ (f.compr‚ÇÇ g) = ofHom‚ÇÇ f ‚â´ ofHom (Linear.rightComp R _ (ofHom g)) := rfl

end ModuleCat
