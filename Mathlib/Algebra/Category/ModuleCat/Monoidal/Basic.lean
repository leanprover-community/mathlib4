/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Kim Morrison, Jakob von Raumer
-/
import Mathlib.Algebra.Category.ModuleCat.Basic
import Mathlib.LinearAlgebra.TensorProduct.Associator
import Mathlib.CategoryTheory.Monoidal.Linear

/-!
# The monoidal category structure on R-modules

Mostly this uses existing machinery in `LinearAlgebra.TensorProduct`.
We just need to provide a few small missing pieces to build the
`MonoidalCategory` instance.
The `SymmetricCategory` instance is in `Algebra.Category.ModuleCat.Monoidal.Symmetric`
to reduce imports.

Note the universe level of the modules must be at least the universe level of the ring,
so that we have a monoidal unit.
For now, we simplify by insisting both universe levels are the same.

We construct the monoidal closed structure on `ModuleCat R` in
`Algebra.Category.ModuleCat.Monoidal.Closed`.

If you're happy using the bundled `ModuleCat R`, it may be possible to mostly
use this as an interface and not need to interact much with the implementation details.
-/

universe v w x u

open CategoryTheory

namespace ModuleCat

variable {R : Type u} [CommRing R]

namespace MonoidalCategory

-- The definitions inside this namespace are essentially private.
-- After we build the `MonoidalCategory (Module R)` instance,
-- you should use that API.
open TensorProduct

attribute [local ext] TensorProduct.ext

/-- (implementation) tensor product of R-modules -/
def tensorObj (M N : ModuleCat R) : ModuleCat R :=
  ModuleCat.of R (M âŠ—[R] N)

/-- (implementation) tensor product of morphisms R-modules -/
def tensorHom {M N M' N' : ModuleCat R} (f : M âŸ¶ N) (g : M' âŸ¶ N') :
    tensorObj M M' âŸ¶ tensorObj N N' :=
  ofHom <| TensorProduct.map f.hom g.hom

/-- (implementation) left whiskering for R-modules -/
def whiskerLeft (M : ModuleCat R) {Nâ‚ Nâ‚‚ : ModuleCat R} (f : Nâ‚ âŸ¶ Nâ‚‚) :
    tensorObj M Nâ‚ âŸ¶ tensorObj M Nâ‚‚ :=
  ofHom <| f.hom.lTensor M

/-- (implementation) right whiskering for R-modules -/
def whiskerRight {Mâ‚ Mâ‚‚ : ModuleCat R} (f : Mâ‚ âŸ¶ Mâ‚‚) (N : ModuleCat R) :
    tensorObj Mâ‚ N âŸ¶ tensorObj Mâ‚‚ N :=
  ofHom <| f.hom.rTensor N

theorem id_tensorHom_id (M N : ModuleCat R) :
    tensorHom (ðŸ™ M) (ðŸ™ N) = ðŸ™ (ModuleCat.of R (M âŠ— N)) := by
  ext : 1
  -- Porting note (https://github.com/leanprover-community/mathlib4/issues/11041): even with high priority `ext` fails to find this.
  apply TensorProduct.ext
  rfl

@[deprecated (since := "2025-07-14")] alias tensor_id := id_tensorHom_id

theorem tensorHom_comp_tensorHom {Xâ‚ Yâ‚ Zâ‚ Xâ‚‚ Yâ‚‚ Zâ‚‚ : ModuleCat R} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚)
    (gâ‚ : Yâ‚ âŸ¶ Zâ‚) (gâ‚‚ : Yâ‚‚ âŸ¶ Zâ‚‚) :
    tensorHom fâ‚ fâ‚‚ â‰« tensorHom gâ‚ gâ‚‚ = tensorHom (fâ‚ â‰« gâ‚) (fâ‚‚ â‰« gâ‚‚) := by
  ext : 1
  -- Porting note (https://github.com/leanprover-community/mathlib4/issues/11041): even with high priority `ext` fails to find this.
  apply TensorProduct.ext
  rfl

/-- (implementation) the associator for R-modules -/
def associator (M : ModuleCat.{v} R) (N : ModuleCat.{w} R) (K : ModuleCat.{x} R) :
    tensorObj (tensorObj M N) K â‰… tensorObj M (tensorObj N K) :=
  (TensorProduct.assoc R M N K).toModuleIso

/-- (implementation) the left unitor for R-modules -/
def leftUnitor (M : ModuleCat.{u} R) : ModuleCat.of R (R âŠ—[R] M) â‰… M :=
  (TensorProduct.lid R M).toModuleIso

/-- (implementation) the right unitor for R-modules -/
def rightUnitor (M : ModuleCat.{u} R) : ModuleCat.of R (M âŠ—[R] R) â‰… M :=
  (TensorProduct.rid R M).toModuleIso

@[simps -isSimp]
instance instMonoidalCategoryStruct : MonoidalCategoryStruct (ModuleCat.{u} R) where
  tensorObj := tensorObj
  whiskerLeft := whiskerLeft
  whiskerRight := whiskerRight
  tensorHom f g := ofHom <| TensorProduct.map f.hom g.hom
  tensorUnit := ModuleCat.of R R
  associator := associator
  leftUnitor := leftUnitor
  rightUnitor := rightUnitor

theorem associator_naturality {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : ModuleCat R} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚)
    (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ) :
    tensorHom (tensorHom fâ‚ fâ‚‚) fâ‚ƒ â‰« (associator Yâ‚ Yâ‚‚ Yâ‚ƒ).hom =
      (associator Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« tensorHom fâ‚ (tensorHom fâ‚‚ fâ‚ƒ) := by
  ext : 1
  apply TensorProduct.ext_threefold
  intro x y z
  rfl

theorem pentagon (W X Y Z : ModuleCat R) :
    whiskerRight (associator W X Y).hom Z â‰«
        (associator W (tensorObj X Y) Z).hom â‰« whiskerLeft W (associator X Y Z).hom =
      (associator (tensorObj W X) Y Z).hom â‰« (associator W X (tensorObj Y Z)).hom := by
  ext : 1
  apply TensorProduct.ext_fourfold
  intro w x y z
  rfl

theorem leftUnitor_naturality {M N : ModuleCat R} (f : M âŸ¶ N) :
    tensorHom (ðŸ™ (ModuleCat.of R R)) f â‰« (leftUnitor N).hom = (leftUnitor M).hom â‰« f := by
  ext : 1
  -- Porting note (https://github.com/leanprover-community/mathlib4/issues/11041): broken ext
  apply TensorProduct.ext
  ext x
  dsimp
  erw [TensorProduct.lid_tmul, TensorProduct.lid_tmul]
  rw [LinearMap.map_smul]
  rfl

theorem rightUnitor_naturality {M N : ModuleCat R} (f : M âŸ¶ N) :
    tensorHom f (ðŸ™ (ModuleCat.of R R)) â‰« (rightUnitor N).hom = (rightUnitor M).hom â‰« f := by
  ext : 1
  -- Porting note (https://github.com/leanprover-community/mathlib4/issues/11041): broken ext
  apply TensorProduct.ext
  ext x
  dsimp
  erw [TensorProduct.rid_tmul, TensorProduct.rid_tmul]
  rw [LinearMap.map_smul]
  rfl

theorem triangle (M N : ModuleCat.{u} R) :
    (associator M (ModuleCat.of R R) N).hom â‰« tensorHom (ðŸ™ M) (leftUnitor N).hom =
      tensorHom (rightUnitor M).hom (ðŸ™ N) := by
  ext : 1
  apply TensorProduct.ext_threefold
  intro x y
  exact TensorProduct.tmul_smul _ _

end MonoidalCategory

open MonoidalCategory

instance monoidalCategory : MonoidalCategory (ModuleCat.{u} R) := MonoidalCategory.ofTensorHom
  (id_tensorHom_id := fun M N â†¦ id_tensorHom_id M N)
  (tensorHom_comp_tensorHom := fun f g h â†¦ MonoidalCategory.tensorHom_comp_tensorHom f g h)
  (associator_naturality := fun f g h â†¦ MonoidalCategory.associator_naturality f g h)
  (leftUnitor_naturality := fun f â†¦ MonoidalCategory.leftUnitor_naturality f)
  (rightUnitor_naturality := fun f â†¦ rightUnitor_naturality f)
  (pentagon := fun M N K L â†¦ pentagon M N K L)
  (triangle := fun M N â†¦ triangle M N)

/-- Remind ourselves that the monoidal unit, being just `R`, is still a commutative ring. -/
instance : CommRing ((ðŸ™_ (ModuleCat.{u} R) : ModuleCat.{u} R) : Type u) :=
  inferInstanceAs <| CommRing R

theorem hom_tensorHom {K L M N : ModuleCat.{u} R} (f : K âŸ¶ L) (g : M âŸ¶ N) :
    (f âŠ—â‚˜ g).hom = TensorProduct.map f.hom g.hom :=
  rfl

theorem hom_whiskerLeft (L : ModuleCat.{u} R) {M N : ModuleCat.{u} R} (f : M âŸ¶ N) :
    (L â— f).hom = f.hom.lTensor L :=
  rfl

theorem hom_whiskerRight {L M : ModuleCat.{u} R} (f : L âŸ¶ M) (N : ModuleCat.{u} R) :
    (f â–· N).hom = f.hom.rTensor N :=
  rfl

theorem hom_hom_leftUnitor {M : ModuleCat.{u} R} :
    (Î»_ M).hom.hom = (TensorProduct.lid _ _).toLinearMap :=
  rfl

theorem hom_inv_leftUnitor {M : ModuleCat.{u} R} :
    (Î»_ M).inv.hom = (TensorProduct.lid _ _).symm.toLinearMap :=
  rfl

theorem hom_hom_rightUnitor {M : ModuleCat.{u} R} :
    (Ï_ M).hom.hom = (TensorProduct.rid _ _).toLinearMap :=
  rfl

theorem hom_inv_rightUnitor {M : ModuleCat.{u} R} :
    (Ï_ M).inv.hom = (TensorProduct.rid _ _).symm.toLinearMap :=
  rfl

theorem hom_hom_associator {M N K : ModuleCat.{u} R} :
    (Î±_ M N K).hom.hom = (TensorProduct.assoc _ _ _ _).toLinearMap :=
  rfl

theorem hom_inv_associator {M N K : ModuleCat.{u} R} :
    (Î±_ M N K).inv.hom = (TensorProduct.assoc _ _ _ _).symm.toLinearMap :=
  rfl

namespace MonoidalCategory

@[simp]
theorem tensorHom_tmul {K L M N : ModuleCat.{u} R} (f : K âŸ¶ L) (g : M âŸ¶ N) (k : K) (m : M) :
    (f âŠ—â‚˜ g) (k âŠ—â‚œ m) = f k âŠ—â‚œ g m :=
  rfl

@[simp]
theorem whiskerLeft_apply (L : ModuleCat.{u} R) {M N : ModuleCat.{u} R} (f : M âŸ¶ N)
    (l : L) (m : M) :
    (L â— f) (l âŠ—â‚œ m) = l âŠ—â‚œ f m :=
  rfl

@[simp]
theorem whiskerRight_apply {L M : ModuleCat.{u} R} (f : L âŸ¶ M) (N : ModuleCat.{u} R)
    (l : L) (n : N) :
    (f â–· N) (l âŠ—â‚œ n) = f l âŠ—â‚œ n :=
  rfl

@[simp]
theorem leftUnitor_hom_apply {M : ModuleCat.{u} R} (r : R) (m : M) :
    ((Î»_ M).hom : ðŸ™_ (ModuleCat R) âŠ— M âŸ¶ M) (r âŠ—â‚œ[R] m) = r â€¢ m :=
  TensorProduct.lid_tmul m r

@[simp]
theorem leftUnitor_inv_apply {M : ModuleCat.{u} R} (m : M) :
    ((Î»_ M).inv : M âŸ¶ ðŸ™_ (ModuleCat.{u} R) âŠ— M) m = 1 âŠ—â‚œ[R] m :=
  TensorProduct.lid_symm_apply m

@[simp]
theorem rightUnitor_hom_apply {M : ModuleCat.{u} R} (m : M) (r : R) :
    ((Ï_ M).hom : M âŠ— ðŸ™_ (ModuleCat R) âŸ¶ M) (m âŠ—â‚œ r) = r â€¢ m :=
  TensorProduct.rid_tmul m r

@[simp]
theorem rightUnitor_inv_apply {M : ModuleCat.{u} R} (m : M) :
    ((Ï_ M).inv : M âŸ¶ M âŠ— ðŸ™_ (ModuleCat.{u} R)) m = m âŠ—â‚œ[R] 1 :=
  TensorProduct.rid_symm_apply m

@[simp]
theorem associator_hom_apply {M N K : ModuleCat.{u} R} (m : M) (n : N) (k : K) :
    ((Î±_ M N K).hom : (M âŠ— N) âŠ— K âŸ¶ M âŠ— N âŠ— K) (m âŠ—â‚œ n âŠ—â‚œ k) = m âŠ—â‚œ (n âŠ—â‚œ k) :=
  rfl

@[simp]
theorem associator_inv_apply {M N K : ModuleCat.{u} R} (m : M) (n : N) (k : K) :
    ((Î±_ M N K).inv : M âŠ— N âŠ— K âŸ¶ (M âŠ— N) âŠ— K) (m âŠ—â‚œ (n âŠ—â‚œ k)) = m âŠ—â‚œ n âŠ—â‚œ k :=
  rfl

variable {Mâ‚ Mâ‚‚ Mâ‚ƒ Mâ‚„ : ModuleCat.{u} R}

section

variable (f : Mâ‚ â†’ Mâ‚‚ â†’ Mâ‚ƒ) (hâ‚ : âˆ€ mâ‚ mâ‚‚ n, f (mâ‚ + mâ‚‚) n = f mâ‚ n + f mâ‚‚ n)
  (hâ‚‚ : âˆ€ (a : R) m n, f (a â€¢ m) n = a â€¢ f m n)
  (hâ‚ƒ : âˆ€ m nâ‚ nâ‚‚, f m (nâ‚ + nâ‚‚) = f m nâ‚ + f m nâ‚‚)
  (hâ‚„ : âˆ€ (a : R) m n, f m (a â€¢ n) = a â€¢ f m n)

/-- Construct for morphisms from the tensor product of two objects in `ModuleCat`. -/
def tensorLift : Mâ‚ âŠ— Mâ‚‚ âŸ¶ Mâ‚ƒ :=
  ofHom <| TensorProduct.lift (LinearMap.mkâ‚‚ R f hâ‚ hâ‚‚ hâ‚ƒ hâ‚„)

@[simp]
lemma tensorLift_tmul (m : Mâ‚) (n : Mâ‚‚) :
    tensorLift f hâ‚ hâ‚‚ hâ‚ƒ hâ‚„ (m âŠ—â‚œ n) = f m n := rfl

end

lemma tensor_ext {f g : Mâ‚ âŠ— Mâ‚‚ âŸ¶ Mâ‚ƒ} (h : âˆ€ m n, f.hom (m âŠ—â‚œ n) = g.hom (m âŠ—â‚œ n)) :
    f = g :=
  hom_ext <| TensorProduct.ext (by ext; apply h)

/-- Extensionality lemma for morphisms from a module of the form `(Mâ‚ âŠ— Mâ‚‚) âŠ— Mâ‚ƒ`. -/
lemma tensor_extâ‚ƒ' {f g : (Mâ‚ âŠ— Mâ‚‚) âŠ— Mâ‚ƒ âŸ¶ Mâ‚„}
    (h : âˆ€ mâ‚ mâ‚‚ mâ‚ƒ, f (mâ‚ âŠ—â‚œ mâ‚‚ âŠ—â‚œ mâ‚ƒ) = g (mâ‚ âŠ—â‚œ mâ‚‚ âŠ—â‚œ mâ‚ƒ)) :
    f = g :=
  hom_ext <| TensorProduct.ext_threefold h

/-- Extensionality lemma for morphisms from a module of the form `Mâ‚ âŠ— (Mâ‚‚ âŠ— Mâ‚ƒ)`. -/
lemma tensor_extâ‚ƒ {f g : Mâ‚ âŠ— (Mâ‚‚ âŠ— Mâ‚ƒ) âŸ¶ Mâ‚„}
    (h : âˆ€ mâ‚ mâ‚‚ mâ‚ƒ, f (mâ‚ âŠ—â‚œ (mâ‚‚ âŠ—â‚œ mâ‚ƒ)) = g (mâ‚ âŠ—â‚œ (mâ‚‚ âŠ—â‚œ mâ‚ƒ))) :
    f = g := by
  rw [â† cancel_epi (Î±_ _ _ _).hom]
  exact tensor_extâ‚ƒ' h

end MonoidalCategory

open Opposite

instance : MonoidalPreadditive (ModuleCat.{u} R) := by
  refine âŸ¨?_, ?_, ?_, ?_âŸ©
  Â· intros
    ext : 1
    refine TensorProduct.ext (LinearMap.ext fun x => LinearMap.ext fun y => ?_)
    simp only [LinearMap.comprâ‚‚â‚›â‚—_apply, TensorProduct.mk_apply, hom_zero, LinearMap.zero_apply]
    -- This used to be `rw`, but we need `erw` after https://github.com/leanprover/lean4/pull/2644
    erw [MonoidalCategory.whiskerLeft_apply]
    simp
  Â· intros
    ext : 1
    refine TensorProduct.ext (LinearMap.ext fun x => LinearMap.ext fun y => ?_)
    simp only [LinearMap.comprâ‚‚â‚›â‚—_apply, TensorProduct.mk_apply, hom_zero, LinearMap.zero_apply, ]
    -- This used to be `rw`, but we need `erw` after https://github.com/leanprover/lean4/pull/2644
    erw [MonoidalCategory.whiskerRight_apply]
    simp
  Â· intros
    ext : 1
    refine TensorProduct.ext (LinearMap.ext fun x => LinearMap.ext fun y => ?_)
    simp only [LinearMap.comprâ‚‚â‚›â‚—_apply, TensorProduct.mk_apply, hom_add, LinearMap.add_apply]
    -- This used to be `rw`, but we need `erw` after https://github.com/leanprover/lean4/pull/2644
    erw [MonoidalCategory.whiskerLeft_apply, MonoidalCategory.whiskerLeft_apply]
    erw [MonoidalCategory.whiskerLeft_apply]
    simp [TensorProduct.tmul_add]
  Â· intros
    ext : 1
    refine TensorProduct.ext (LinearMap.ext fun x => LinearMap.ext fun y => ?_)
    simp only [LinearMap.comprâ‚‚â‚›â‚—_apply, TensorProduct.mk_apply, hom_add, LinearMap.add_apply]
    -- This used to be `rw`, but we need `erw` after https://github.com/leanprover/lean4/pull/2644
    erw [MonoidalCategory.whiskerRight_apply, MonoidalCategory.whiskerRight_apply]
    erw [MonoidalCategory.whiskerRight_apply]
    simp [TensorProduct.add_tmul]

instance : MonoidalLinear R (ModuleCat.{u} R) := by
  refine âŸ¨?_, ?_âŸ©
  Â· intros
    ext : 1
    refine TensorProduct.ext (LinearMap.ext fun x => LinearMap.ext fun y => ?_)
    simp only [LinearMap.comprâ‚‚â‚›â‚—_apply, TensorProduct.mk_apply, hom_smul, LinearMap.smul_apply]
    -- This used to be `rw`, but we need `erw` after https://github.com/leanprover/lean4/pull/2644
    erw [MonoidalCategory.whiskerLeft_apply, MonoidalCategory.whiskerLeft_apply]
    simp
  Â· intros
    ext : 1
    refine TensorProduct.ext (LinearMap.ext fun x => LinearMap.ext fun y => ?_)
    simp only [LinearMap.comprâ‚‚â‚›â‚—_apply, TensorProduct.mk_apply, hom_smul, LinearMap.smul_apply]
    -- This used to be `rw`, but we need `erw` after https://github.com/leanprover/lean4/pull/2644
    erw [MonoidalCategory.whiskerRight_apply, MonoidalCategory.whiskerRight_apply]
    simp [TensorProduct.smul_tmul, TensorProduct.tmul_smul]

@[simp] lemma ofHomâ‚‚_comprâ‚‚ {M N P Q : ModuleCat.{u} R} (f : M â†’â‚—[R] N â†’â‚—[R] P) (g : P â†’â‚—[R] Q) :
    ofHomâ‚‚ (f.comprâ‚‚ g) = ofHomâ‚‚ f â‰« ofHom (Linear.rightComp R _ (ofHom g)) := rfl

end ModuleCat
