/-
Copyright (c) 2025 Junyan Xu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robert A. Spencer, Junyan Xu
-/
import Mathlib.Algebra.Algebra.Defs
import Mathlib.Algebra.BigOperators.Group.Finset.Defs
import Mathlib.Algebra.Category.MonCat.Basic
import Mathlib.Algebra.Module.Equiv.Basic
import Mathlib.Algebra.Module.PUnit
import Mathlib.CategoryTheory.Conj
import Mathlib.CategoryTheory.Limits.Shapes.ZeroMorphisms

/-!
# The category of `R`-modules

If `R` is a semiring, `SemimoduleCat.{v} R` is the category of bundled `R`-modules with carrier
in the universe `v`. We  show that it is preadditive and show that being an isomorphism and
monomorphism are equivalent to being a linear equivalence and an injective linear map respectively.

## Implementation details

To construct an object in the category of `R`-modules from a type `M` with an instance of the
`Module` typeclass, write `of R M`. There is a coercion in the other direction.
The roundtrip `‚Üë(of R M)` is definitionally equal to `M` itself (when `M` is a type with `Module`
instance), and so is `of R ‚ÜëM` (when `M : SemimoduleCat R M`).

The morphisms are given their own type, not identified with `LinearMap`.
There is a cast from morphisms in `Module R` to linear maps,
written `f.hom` (`SemimoduleCat.Hom.hom`).
To go from linear maps to morphisms in `Module R`, use `SemimoduleCat.ofHom`.

Similarly, given an isomorphism `f : M ‚âÖ N` use `f.toLinearEquiv` and given a linear equiv
`f : M ‚âÉ‚Çó[R] N`, use `f.toModuleIso`.
-/


open CategoryTheory Limits WalkingParallelPair

universe v u

variable (R : Type u) [Semiring R]

/-- The category of R-modules and their morphisms.

Note that in the case of `R = ‚Ñï`, we can not
impose here that the `‚Ñï`-multiplication field from the module structure is defeq to the one coming
from the `isAddCommMonoid` structure (contrary to what we do for all module structures in
mathlib), which creates some difficulties down the road. -/
structure SemimoduleCat where
  private mk ::
  /-- the underlying type of an object in `SemimoduleCat R` -/
  carrier : Type v
  [isAddCommMonoid : AddCommMonoid carrier]
  [isModule : Module R carrier]

attribute [instance] SemimoduleCat.isAddCommMonoid SemimoduleCat.isModule

namespace SemimoduleCat

instance : CoeSort (SemimoduleCat.{v} R) (Type v) :=
  ‚ü®SemimoduleCat.carrier‚ü©

attribute [coe] SemimoduleCat.carrier

/-- The object in the category of R-algebras associated to a type equipped with the appropriate
typeclasses. This is the preferred way to construct a term of `SemimoduleCat R`. -/
abbrev of (X : Type v) [AddCommMonoid X] [Module R X] : SemimoduleCat.{v} R :=
  ‚ü®X‚ü©

lemma coe_of (X : Type v) [Semiring X] [Module R X] : (of R X : Type v) = X :=
  rfl

-- Ensure the roundtrips are reducibly defeq (so tactics like `rw` can see through them).
example (X : Type v) [Semiring X] [Module R X] : (of R X : Type v) = X := by with_reducible rfl
example (M : SemimoduleCat.{v} R) : of R M = M := by with_reducible rfl

variable {R} in
/-- The type of morphisms in `SemimoduleCat R`. -/
@[ext]
structure Hom (M N : SemimoduleCat.{v} R) where
  mk ::
  /-- The underlying linear map. -/
  hom' : M ‚Üí‚Çó[R] N

instance moduleCategory : Category.{v, max (v+1) u} (SemimoduleCat.{v} R) where
  Hom M N := Hom M N
  id _ := ‚ü®LinearMap.id‚ü©
  comp f g := ‚ü®g.hom'.comp f.hom'‚ü©

instance : ConcreteCategory (SemimoduleCat.{v} R) (¬∑ ‚Üí‚Çó[R] ¬∑) where
  hom := Hom.hom'
  ofHom := Hom.mk

section

variable {R}

/-- Turn a morphism in `SemimoduleCat` back into a `LinearMap`. -/
abbrev Hom.hom {A B : SemimoduleCat.{v} R} (f : Hom A B) :=
  ConcreteCategory.hom (C := SemimoduleCat R) f

/-- Typecheck a `LinearMap` as a morphism in `SemimoduleCat`. -/
abbrev ofHom {X Y : Type v} [AddCommMonoid X] [Module R X] [AddCommMonoid Y] [Module R Y]
    (f : X ‚Üí‚Çó[R] Y) : of R X ‚ü∂ of R Y :=
  ConcreteCategory.ofHom (C := SemimoduleCat R) f

/-- Use the `ConcreteCategory.hom` projection for `@[simps]` lemmas. -/
def Hom.Simps.hom (A B : SemimoduleCat.{v} R) (f : Hom A B) :=
  f.hom

initialize_simps_projections Hom (hom' ‚Üí hom)

/-!
The results below duplicate the `ConcreteCategory` simp lemmas, but we can keep them for `dsimp`.
-/

@[simp]
lemma hom_id {M : SemimoduleCat.{v} R} : (ùüô M : M ‚ü∂ M).hom = LinearMap.id := rfl

/- Provided for rewriting. -/
lemma id_apply (M : SemimoduleCat.{v} R) (x : M) :
    (ùüô M : M ‚ü∂ M) x = x := by simp

@[simp]
lemma hom_comp {M N O : SemimoduleCat.{v} R} (f : M ‚ü∂ N) (g : N ‚ü∂ O) :
    (f ‚â´ g).hom = g.hom.comp f.hom := rfl

/- Provided for rewriting. -/
lemma comp_apply {M N O : SemimoduleCat.{v} R} (f : M ‚ü∂ N) (g : N ‚ü∂ O) (x : M) :
    (f ‚â´ g) x = g (f x) := by simp

@[ext]
lemma hom_ext {M N : SemimoduleCat.{v} R} {f g : M ‚ü∂ N} (hf : f.hom = g.hom) : f = g :=
  Hom.ext hf

lemma hom_bijective {M N : SemimoduleCat.{v} R} :
    Function.Bijective (Hom.hom : (M ‚ü∂ N) ‚Üí (M ‚Üí‚Çó[R] N)) where
  left f g h := by cases f; cases g; simpa using h
  right f := ‚ü®‚ü®f‚ü©, rfl‚ü©

/-- Convenience shortcut for `SemimoduleCat.hom_bijective.injective`. -/
lemma hom_injective {M N : SemimoduleCat.{v} R} :
    Function.Injective (Hom.hom : (M ‚ü∂ N) ‚Üí (M ‚Üí‚Çó[R] N)) :=
  hom_bijective.injective

/-- Convenience shortcut for `SemimoduleCat.hom_bijective.surjective`. -/
lemma hom_surjective {M N : SemimoduleCat.{v} R} :
    Function.Surjective (Hom.hom : (M ‚ü∂ N) ‚Üí (M ‚Üí‚Çó[R] N)) :=
  hom_bijective.surjective

@[simp]
lemma hom_ofHom {X Y : Type v} [AddCommMonoid X] [Module R X] [AddCommMonoid Y]
    [Module R Y] (f : X ‚Üí‚Çó[R] Y) : (ofHom f).hom = f := rfl

@[simp]
lemma ofHom_hom {M N : SemimoduleCat.{v} R} (f : M ‚ü∂ N) :
    ofHom (Hom.hom f) = f := rfl

@[simp]
lemma ofHom_id {M : Type v} [AddCommMonoid M] [Module R M] : ofHom LinearMap.id = ùüô (of R M) := rfl

@[simp]
lemma ofHom_comp {M N O : Type v} [AddCommMonoid M] [AddCommMonoid N] [AddCommMonoid O] [Module R M]
    [Module R N] [Module R O] (f : M ‚Üí‚Çó[R] N) (g : N ‚Üí‚Çó[R] O) :
    ofHom (g.comp f) = ofHom f ‚â´ ofHom g :=
  rfl

/- Doesn't need to be `@[simp]` since `simp only` can solve this. -/
lemma ofHom_apply {M N : Type v} [AddCommMonoid M] [AddCommMonoid N] [Module R M] [Module R N]
    (f : M ‚Üí‚Çó[R] N) (x : M) : ofHom f x = f x := rfl

lemma inv_hom_apply {M N : SemimoduleCat.{v} R} (e : M ‚âÖ N) (x : M) : e.inv (e.hom x) = x := by
  simp

lemma hom_inv_apply {M N : SemimoduleCat.{v} R} (e : M ‚âÖ N) (x : N) : e.hom (e.inv x) = x := by
  simp

/-- `SemimoduleCat.Hom.hom` bundled as an `Equiv`. -/
def homEquiv {M N : SemimoduleCat.{v} R} : (M ‚ü∂ N) ‚âÉ (M ‚Üí‚Çó[R] N) where
  toFun := Hom.hom
  invFun := ofHom

end

/- Not a `@[simp]` lemma since it will rewrite the (co)domain of maps and cause
definitional equality issues. -/
lemma forget_obj {M : SemimoduleCat.{v} R} : (forget (SemimoduleCat.{v} R)).obj M = M := rfl

@[simp]
lemma forget_map {M N : SemimoduleCat.{v} R} (f : M ‚ü∂ N) :
    (forget (SemimoduleCat.{v} R)).map f = f :=
  rfl

instance hasForgetToAddCommMonoid : HasForget‚ÇÇ (SemimoduleCat R) AddCommMonCat where
  forget‚ÇÇ :=
    { obj := fun M => .of M
      map := fun f => AddCommMonCat.ofHom f.hom.toAddMonoidHom }

@[simp]
theorem forget‚ÇÇ_obj (X : SemimoduleCat R) :
    (forget‚ÇÇ (SemimoduleCat R) AddCommMonCat).obj X = .of X :=
  rfl

theorem forget‚ÇÇ_obj_moduleCat_of (X : Type v) [AddCommMonoid X] [Module R X] :
    (forget‚ÇÇ (SemimoduleCat R) AddCommMonCat).obj (of R X) = .of X :=
  rfl

@[simp]
theorem forget‚ÇÇ_map (X Y : SemimoduleCat R) (f : X ‚ü∂ Y) :
    (forget‚ÇÇ (SemimoduleCat R) AddCommMonCat).map f = AddCommMonCat.ofHom f.hom :=
  rfl

instance : Inhabited (SemimoduleCat R) :=
  ‚ü®of R PUnit‚ü©

@[simp] theorem of_coe (X : SemimoduleCat R) : of R X = X := rfl

variable {R}

/-- Forgetting to the underlying type and then building the bundled object returns the original
module. -/
@[deprecated Iso.refl (since := "2025-05-15")]
def ofSelfIso (M : SemimoduleCat R) : SemimoduleCat.of R M ‚âÖ M where
  hom := ùüô M
  inv := ùüô M

theorem isZero_of_subsingleton (M : SemimoduleCat R) [Subsingleton M] : IsZero M where
  unique_to X := ‚ü®‚ü®‚ü®ofHom (0 : M ‚Üí‚Çó[R] X)‚ü©, fun f => by
    ext x
    rw [Subsingleton.elim x (0 : M)]
    simp‚ü©‚ü©
  unique_from X := ‚ü®‚ü®‚ü®ofHom (0 : X ‚Üí‚Çó[R] M)‚ü©, fun f => by
    ext x
    subsingleton‚ü©‚ü©

instance : HasZeroObject (SemimoduleCat.{v} R) :=
  ‚ü®‚ü®of R PUnit, isZero_of_subsingleton _‚ü©‚ü©

end SemimoduleCat

variable {R}
variable {X‚ÇÅ X‚ÇÇ : Type v}

open SemimoduleCat

/-- Reinterpreting a linear map in the category of `R`-modules -/
scoped[SemimoduleCat] notation "‚Üü" f:1024 => SemimoduleCat.ofHom f

section

/-- Build an isomorphism in the category `Module R` from a `LinearEquiv` between `Module`s. -/
@[simps]
def LinearEquiv.toModuleIso‚Çõ {g‚ÇÅ : AddCommMonoid X‚ÇÅ} {g‚ÇÇ : AddCommMonoid X‚ÇÇ} {m‚ÇÅ : Module R X‚ÇÅ}
    {m‚ÇÇ : Module R X‚ÇÇ} (e : X‚ÇÅ ‚âÉ‚Çó[R] X‚ÇÇ) : SemimoduleCat.of R X‚ÇÅ ‚âÖ SemimoduleCat.of R X‚ÇÇ where
  hom := ofHom (e : X‚ÇÅ ‚Üí‚Çó[R] X‚ÇÇ)
  inv := ofHom (e.symm : X‚ÇÇ ‚Üí‚Çó[R] X‚ÇÅ)
  hom_inv_id := by ext; apply e.left_inv
  inv_hom_id := by ext; apply e.right_inv

namespace CategoryTheory.Iso

/-- Build a `LinearEquiv` from an isomorphism in the category `SemimoduleCat R`. -/
def toLinearEquiv‚Çõ {X Y : SemimoduleCat R} (i : X ‚âÖ Y) : X ‚âÉ‚Çó[R] Y :=
  LinearEquiv.ofLinear i.hom.hom i.inv.hom (by aesop) (by aesop)

end CategoryTheory.Iso

/-- linear equivalences between `Module`s are the same as (isomorphic to) isomorphisms
in `SemimoduleCat` -/
@[simps]
def linearEquivIsoModuleIso‚Çõ {X Y : Type u} [AddCommMonoid X] [AddCommMonoid Y] [Module R X]
    [Module R Y] : (X ‚âÉ‚Çó[R] Y) ‚âÖ SemimoduleCat.of R X ‚âÖ SemimoduleCat.of R Y where
  hom e := e.toModuleIso‚Çõ
  inv i := i.toLinearEquiv‚Çõ

end

namespace SemimoduleCat

section AddCommMonoid

variable {M N : SemimoduleCat.{v} R}

instance : Add (M ‚ü∂ N) where
  add f g := ‚ü®f.hom + g.hom‚ü©

@[simp] lemma hom_add (f g : M ‚ü∂ N) : (f + g).hom = f.hom + g.hom := rfl

instance : Zero (M ‚ü∂ N) where
  zero := ‚ü®0‚ü©

@[simp] lemma hom_zero : (0 : M ‚ü∂ N).hom = 0 := rfl

instance : SMul ‚Ñï (M ‚ü∂ N) where
  smul n f := ‚ü®n ‚Ä¢ f.hom‚ü©

@[simp] lemma hom_nsmul (n : ‚Ñï) (f : M ‚ü∂ N) : (n ‚Ä¢ f).hom = n ‚Ä¢ f.hom := rfl

instance : SMul ‚Ñï (M ‚ü∂ N) where
  smul n f := ‚ü®n ‚Ä¢ f.hom‚ü©

@[simp] lemma hom_zsmul (n : ‚Ñï) (f : M ‚ü∂ N) : (n ‚Ä¢ f).hom = n ‚Ä¢ f.hom := rfl

instance : AddCommMonoid (M ‚ü∂ N) :=
  Function.Injective.addCommMonoid Hom.hom hom_injective rfl (fun _ _ => rfl) (fun _ _ => rfl)

@[simp] lemma hom_sum {Œπ : Type*} (f : Œπ ‚Üí (M ‚ü∂ N)) (s : Finset Œπ) :
    (‚àë i ‚àà s, f i).hom = ‚àë i ‚àà s, (f i).hom :=
  map_sum ({ toFun := SemimoduleCat.Hom.hom, map_zero' := SemimoduleCat.hom_zero,
             map_add' := hom_add } : (M ‚ü∂ N) ‚Üí+ (M ‚Üí‚Çó[R] N)) _ _

/- TODO: generalize Preadditive and Functor.Additive, see #28826.
instance : Presemiadditive (SemimoduleCat.{v} R) where
instance : (forget‚ÇÇ (SemimoduleCat.{v} R) AddCommMonCat).Additive where -/

instance : HasZeroMorphisms (SemimoduleCat.{v} R) where

/-- `SemimoduleCat.Hom.hom` bundled as an additive equivalence. -/
@[simps!]
def homAddEquiv : (M ‚ü∂ N) ‚âÉ+ (M ‚Üí‚Çó[R] N) :=
  { homEquiv with
    map_add' := fun _ _ => rfl }

theorem subsingleton_of_isZero (h : IsZero M) : Subsingleton M := by
  refine subsingleton_of_forall_eq 0 (fun x ‚Ü¶ ?_)
  rw [‚Üê LinearMap.id_apply (R := R) x, ‚Üê SemimoduleCat.hom_id]
  simp only [(CategoryTheory.Limits.IsZero.iff_id_eq_zero M).mp h, hom_zero, LinearMap.zero_apply]

lemma isZero_iff_subsingleton : IsZero M ‚Üî Subsingleton M where
  mp := subsingleton_of_isZero
  mpr _ := isZero_of_subsingleton M

@[simp]
lemma isZero_of_iff_subsingleton {M : Type*} [AddCommMonoid M] [Module R M] :
    IsZero (of R M) ‚Üî Subsingleton M := isZero_iff_subsingleton

end AddCommMonoid

section SMul

variable {M N : SemimoduleCat.{v} R}
variable {S : Type*} [Monoid S] [DistribMulAction S N] [SMulCommClass R S N]

instance : SMul S (M ‚ü∂ N) where
  smul c f := ‚ü®c ‚Ä¢ f.hom‚ü©

@[simp] lemma hom_smul (s : S) (f : M ‚ü∂ N) : (s ‚Ä¢ f).hom = s ‚Ä¢ f.hom := rfl

end SMul

section Module

variable {M N : SemimoduleCat.{v} R} {S : Type*} [Semiring S] [Module S N] [SMulCommClass R S N]

instance Hom.instModule : Module S (M ‚ü∂ N) :=
  Function.Injective.module S
    { toFun := Hom.hom, map_zero' := hom_zero, map_add' := hom_add }
    hom_injective
    (fun _ _ => rfl)

/-- `SemimoduleCat.Hom.hom` bundled as a linear equivalence. -/
@[simps]
def homLinearEquiv : (M ‚ü∂ N) ‚âÉ‚Çó[S] (M ‚Üí‚Çó[R] N) :=
  { homAddEquiv with
    map_smul' := fun _ _ => rfl }

end Module

section

universe u‚ÇÄ

namespace Algebra

variable {S‚ÇÄ : Type u‚ÇÄ} [CommSemiring S‚ÇÄ] {S : Type u} [Semiring S] [Algebra S‚ÇÄ S]

variable {M N : SemimoduleCat.{v} S}

/--
Let `S` be an `S‚ÇÄ`-algebra. Then `S`-modules are modules over `S‚ÇÄ`.
-/
scoped instance : Module S‚ÇÄ M := Module.compHom _ (algebraMap S‚ÇÄ S)

scoped instance : IsScalarTower S‚ÇÄ S M where
  smul_assoc _ _ _ := by rw [Algebra.smul_def, mul_smul]; rfl

scoped instance : SMulCommClass S S‚ÇÄ M where
  smul_comm s s‚ÇÄ n :=
    show s ‚Ä¢ algebraMap S‚ÇÄ S s‚ÇÄ ‚Ä¢ n = algebraMap S‚ÇÄ S s‚ÇÄ ‚Ä¢ s ‚Ä¢ n by
    rw [‚Üê smul_assoc, smul_eq_mul, ‚Üê Algebra.commutes, mul_smul]

/- TODO: generalize `Functor.Linear`, see #28826.
Let `S` be an `S‚ÇÄ`-algebra. Then the category of `S`-modules is `S‚ÇÄ`-linear.
scoped instance instLinear : Linear S‚ÇÄ (SemimoduleCat.{v} S) where
  smul_comp _ M N s‚ÇÄ f g := by ext; simp -/

end Algebra

section

variable {S : Type u} [CommSemiring S]

/- TODO: generalize `Functor.Linear`, see #28826.
instance : Linear S (SemimoduleCat.{v} S) := SemimoduleCat.Algebra.instLinear -/

variable {X Y X' Y' : SemimoduleCat.{v} S}

theorem Iso.homCongr_eq_arrowCongr (i : X ‚âÖ X') (j : Y ‚âÖ Y') (f : X ‚ü∂ Y) :
    Iso.homCongr i j f = ‚ü®LinearEquiv.arrowCongr i.toLinearEquiv‚Çõ j.toLinearEquiv‚Çõ f.hom‚ü© :=
  rfl

theorem Iso.conj_eq_conj (i : X ‚âÖ X') (f : End X) :
    Iso.conj i f = ‚ü®LinearEquiv.conj i.toLinearEquiv‚Çõ f.hom‚ü© :=
  rfl

end

end

/- TODO: Declarations in #28826 from `endSemiringEquiv` to `forget‚ÇÇ_map_homMk` can be added back
after appropriate generalizations. -/

instance : (forget (SemimoduleCat.{v} R)).ReflectsIsomorphisms where
  reflects f _ :=
    (inferInstance : IsIso ((LinearEquiv.mk f.hom
      (asIso ((forget (SemimoduleCat R)).map f)).toEquiv.invFun
      (Equiv.left_inv _) (Equiv.right_inv _)).toModuleIso‚Çõ).hom)

instance : (forget‚ÇÇ (SemimoduleCat.{v} R) AddCommMonCat.{v}).ReflectsIsomorphisms where
  reflects f _ := by
    have : IsIso ((forget _).map f) := by
      change IsIso ((forget _).map ((forget‚ÇÇ _ AddCommMonCat).map f))
      infer_instance
    apply isIso_of_reflects_iso _ (forget _)

end SemimoduleCat

section Bilinear

variable {R : Type*} [CommSemiring R]

namespace SemimoduleCat

/-- Turn a bilinear map into a homomorphism. -/
@[simps!]
def ofHom‚ÇÇ {M N P : SemimoduleCat.{u} R} (f : M ‚Üí‚Çó[R] N ‚Üí‚Çó[R] P) :
    M ‚ü∂ of R (N ‚ü∂ P) :=
  ofHom <| homLinearEquiv.symm.toLinearMap ‚àò‚Çó f

/-- Turn a homomorphism into a bilinear map. -/
@[simps!]
def Hom.hom‚ÇÇ {M N P : SemimoduleCat.{u} R}
    -- We write `Hom` instead of `M ‚ü∂ (of R (N ‚ü∂ P))`, otherwise dot notation breaks
    -- since it is expecting the type of `f` to be `SemimoduleCat.Hom`, not `Quiver.Hom`.
    (f : Hom M (of R (N ‚ü∂ P))) :
    M ‚Üí‚Çó[R] N ‚Üí‚Çó[R] P :=
  Hom.hom (by convert (f ‚â´ ofHom homLinearEquiv.toLinearMap))

@[simp] lemma Hom.hom‚ÇÇ_ofHom‚ÇÇ {M N P : SemimoduleCat.{u} R} (f : M ‚Üí‚Çó[R] N ‚Üí‚Çó[R] P) :
    (ofHom‚ÇÇ f).hom‚ÇÇ = f := rfl

@[simp] lemma ofHom‚ÇÇ_hom‚ÇÇ {M N P : SemimoduleCat.{u} R} (f : M ‚ü∂ of R (N ‚ü∂ P)) :
    ofHom‚ÇÇ f.hom‚ÇÇ = f := rfl

end SemimoduleCat

end Bilinear

/-!
`@[simp]` lemmas for `LinearMap.comp` and categorical identities.
-/

@[simp] theorem LinearMap.comp_id_semiModuleCat {R} [Semiring R]
    {G : SemimoduleCat.{u} R} {H : Type u} [AddCommMonoid H] [Module R H] (f : G ‚Üí‚Çó[R] H) :
    f.comp (ùüô G : G ‚ü∂ G).hom = f := by simp

@[simp] theorem LinearMap.id_semiModuleCat_comp {R} [Semiring R]
    {G : Type u} [AddCommMonoid G] [Module R G] {H : SemimoduleCat.{u} R} (f : G ‚Üí‚Çó[R] H) :
    LinearMap.comp (ùüô H : H ‚ü∂ H).hom f = f := by simp
