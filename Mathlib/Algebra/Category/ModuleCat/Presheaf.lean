/-
Copyright (c) 2023 Scott Morrison All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Algebra.Category.Ring.Basic
import Mathlib.Algebra.Category.ModuleCat.ChangeOfRings

/-!
# Presheaves of modules over a presheaf of rings.

We give a hands-on description of a presheaf of modules over a fixed presheaf of rings `R`,
as a presheaf of abelian groups with additional data.

We also provide two alternative constructors :
* `mk' : CorePresheafOfModules R â†’ PresheafOfModules R` where `M : CorePresheafOfModules R`
consists of a family of unbundled modules over `R.obj X` for all `X`
* `mk'' : BundledCorePresheafOfModules R â†’ PresheafOfModules R`
where `M : BundledCorePresheafOfModules R` consists of a family of objects in
`ModuleCat (R.obj X)` for all `X`

## Future work

* Compare this to the definition as a presheaf of pairs `(R, M)` with specified first part.
* Compare this to the definition as a module object of the presheaf of rings
  thought of as a monoid object.
* (Pre)sheaves of modules over a given sheaf of rings are an abelian category.
* Presheaves of modules over a presheaf of commutative rings form a monoidal category.
* Pushforward and pullback.
-/

universe v vâ‚ uâ‚ u

open CategoryTheory LinearMap Opposite

variable {C : Type uâ‚} [Category.{vâ‚} C]

/-- A presheaf of modules over a given presheaf of rings,
described as a presheaf of abelian groups, and the extra data of the action at each object,
and a condition relating functoriality and scalar multiplication. -/
structure PresheafOfModules (R : Cáµ’áµ– â¥¤ RingCat.{u}) where
  presheaf : Cáµ’áµ– â¥¤ AddCommGroupCat.{v}
  module : âˆ€ X : Cáµ’áµ–, Module (R.obj X) (presheaf.obj X) := by infer_instance
  map_smul : âˆ€ {X Y : Cáµ’áµ–} (f : X âŸ¶ Y) (r : R.obj X) (x : presheaf.obj X),
    presheaf.map f (r â€¢ x) = R.map f r â€¢ presheaf.map f x := by aesop_cat

variable {R : Cáµ’áµ– â¥¤ RingCat.{u}}

namespace PresheafOfModules

attribute [instance] PresheafOfModules.module

/-- The bundled module over an object `X`. -/
def obj (P : PresheafOfModules R) (X : Cáµ’áµ–) : ModuleCat (R.obj X) :=
  ModuleCat.of _ (P.presheaf.obj X)

/--
If `P` is a presheaf of modules over a presheaf of rings `R`, both over some category `C`,
and `f : X âŸ¶ Y` is a morphism in `Cáµ’áµ–`, we construct the `R.map f`-semilinear map
from the `R.obj X`-module `P.presheaf.obj X` to the `R.obj Y`-module `P.presheaf.obj Y`.
 -/
def map (P : PresheafOfModules R) {X Y : Cáµ’áµ–} (f : X âŸ¶ Y) :
    P.obj X â†’â‚›â‚—[R.map f] P.obj Y :=
  { toAddHom := (P.presheaf.map f).toAddHom,
    map_smul' := P.map_smul f, }

@[simp]
theorem map_apply (P : PresheafOfModules R) {X Y : Cáµ’áµ–} (f : X âŸ¶ Y) (x) :
    P.map f x = (P.presheaf.map f) x :=
  rfl

instance (X : Cáµ’áµ–) : RingHomId (R.map (ðŸ™ X)) where
  eq_id := R.map_id X

instance {X Y Z : Cáµ’áµ–} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    RingHomCompTriple (R.map f) (R.map g) (R.map (f â‰« g)) where
  comp_eq := (R.map_comp f g).symm

@[simp]
theorem map_id (P : PresheafOfModules R) (X : Cáµ’áµ–) :
    P.map (ðŸ™ X) = LinearMap.id' := by
  ext
  simp

@[simp]
theorem map_comp (P : PresheafOfModules R) {X Y Z : Cáµ’áµ–} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    P.map (f â‰« g) = (P.map g).comp (P.map f) := by
  ext
  simp

/-- A morphism of presheaves of modules. -/
structure Hom (P Q : PresheafOfModules R) where
  hom : P.presheaf âŸ¶ Q.presheaf
  map_smul : âˆ€ (X : Cáµ’áµ–) (r : R.obj X) (x : P.presheaf.obj X), hom.app X (r â€¢ x) = r â€¢ hom.app X x

namespace Hom

/-- The identity morphism on a presheaf of modules. -/
def id (P : PresheafOfModules R) : Hom P P where
  hom := ðŸ™ _
  map_smul _ _ _ := rfl

/-- Composition of morphisms of presheaves of modules. -/
def comp {P Q R : PresheafOfModules R} (f : Hom P Q) (g : Hom Q R) : Hom P R where
  hom := f.hom â‰« g.hom
  map_smul _ _ _ := by simp [Hom.map_smul]

end Hom

instance : Category (PresheafOfModules R) where
  Hom := Hom
  id := Hom.id
  comp f g := Hom.comp f g

namespace Hom

variable {P Q T : PresheafOfModules R}

/--
The `(X : Cáµ’áµ–)`-component of morphism between presheaves of modules
over a presheaf of rings `R`, as an `R.obj X`-linear map. -/
def app (f : Hom P Q) (X : Cáµ’áµ–) : P.obj X â†’â‚—[R.obj X] Q.obj X :=
  { toAddHom := (f.hom.app X).toAddHom
    map_smul' := f.map_smul X }

@[simp]
lemma comp_app (f : P âŸ¶ Q) (g : Q âŸ¶ T) (X : Cáµ’áµ–) :
    (f â‰« g).app X = (g.app X).comp (f.app X) := rfl

@[ext]
theorem ext {f g : P âŸ¶ Q} (w : âˆ€ X, f.app X = g.app X) : f = g := by
  cases f; cases g
  congr
  ext X x
  exact LinearMap.congr_fun (w X) x

section

variable (app : âˆ€ X, P.obj X â†’â‚—[R.obj X] Q.obj X)
  (naturality : âˆ€ â¦ƒX Y : Cáµ’áµ–â¦„ (f : X âŸ¶ Y) (x : P.obj X),
    app Y (P.map f x) = Q.map f (app X x))

/-- A constructor for morphisms in `PresheafOfModules R` that is based on the data
of a family of linear maps over the various rings `R.obj X`. -/
def mk' : P âŸ¶ Q where
  hom :=
    { app := fun X => (app X).toAddMonoidHom
      naturality := fun X Y f => by ext x; apply naturality }
  map_smul X := (app X).map_smul

@[simp]
lemma mk'_app : (mk' app naturality).app = app := rfl

end

instance : Zero (P âŸ¶ Q) := âŸ¨mk 0 (by
  intros
  simp only [Limits.zero_app, AddMonoidHom.zero_apply, smul_zero])âŸ©

variable (P Q)

@[simp]
lemma zero_app (X : Cáµ’áµ–) : (0 : P âŸ¶ Q).app X = 0 := rfl

variable {P Q}

instance : Add (P âŸ¶ Q) := âŸ¨fun f g => mk (f.hom + g.hom) (by
  intros
  simp only [NatTrans.app_add, AddCommGroupCat.hom_add_apply, map_smul, smul_add])âŸ©

@[simp]
lemma add_app (f g : P âŸ¶ Q) (X : Cáµ’áµ–) : (f + g).app X = f.app X + g.app X := rfl

instance : Sub (P âŸ¶ Q) := âŸ¨fun f g => mk (f.hom - g.hom) (by
  intros
  rw [NatTrans.app_sub, AddMonoidHom.sub_apply, AddMonoidHom.sub_apply,
    smul_sub, map_smul, map_smul])âŸ©

@[simp]
lemma sub_app (f g : P âŸ¶ Q) (X : Cáµ’áµ–) : (f - g).app X = f.app X - g.app X := rfl

instance : Neg (P âŸ¶ Q) := âŸ¨fun f => mk (-f.hom) (by
  intros
  rw [NatTrans.app_neg, AddMonoidHom.neg_apply, AddMonoidHom.neg_apply,
    map_smul, smul_neg])âŸ©

@[simp]
lemma neg_app (f : P âŸ¶ Q) (X : Cáµ’áµ–): (-f).app X = -f.app X := rfl

instance : AddCommGroup (P âŸ¶ Q) where
  add_assoc := by intros; ext1; simp only [add_app, add_assoc]
  zero_add := by intros; ext1; simp only [add_app, zero_app, zero_add]
  add_left_neg := by intros; ext1; simp only [add_app, neg_app, add_left_neg, zero_app]
  add_zero := by intros; ext1; simp only [add_app, zero_app, add_zero]
  add_comm := by intros; ext1; simp only [add_app]; apply add_comm
  sub_eq_add_neg := by intros; ext1; simp only [add_app, sub_app, neg_app, sub_eq_add_neg]
  nsmul := nsmulRec
  zsmul := zsmulRec

instance : Preadditive (PresheafOfModules R) where
  add_comp := by intros; ext1; simp only [comp_app, add_app, comp_add]
  comp_add := by intros; ext1; simp only [comp_app, add_app, add_comp]

end Hom

variable (R)

/-- The functor from presheaves of modules over a specified presheaf of rings,
to presheaves of abelian groups.
-/
@[simps obj]
def toPresheaf : PresheafOfModules R â¥¤ (Cáµ’áµ– â¥¤ AddCommGroupCat) where
  obj P := P.presheaf
  map f := f.hom

variable {R}

@[simp]
lemma toPresheaf_map_app {P Q : PresheafOfModules R}
    (f : P âŸ¶ Q) (X : Cáµ’áµ–) :
    ((toPresheaf R).map f).app X = (f.app X).toAddMonoidHom := rfl

instance : (toPresheaf R).Additive where

instance : Faithful (toPresheaf R) where
  map_injective {P Q} f g h := by
    ext X x
    have eq := congr_app h X
    simp only [toPresheaf_obj, toPresheaf_map_app] at eq
    simp only [â† toAddMonoidHom_coe, eq]

variable (R)

/-- Evaluation on an object `X` gives a functor
`PresheafOfModules R â¥¤ ModuleCat (R.obj X)`. -/
@[simps]
def evaluation (X : Cáµ’áµ–) : PresheafOfModules.{v} R â¥¤ ModuleCat (R.obj X) where
  obj M := M.obj X
  map f := f.app X

/-- The restriction natural transformation on presheaves of modules, considered as linear maps
to restriction of scalars. -/
noncomputable def restriction {X Y : Cáµ’áµ–} (f : X âŸ¶ Y) :
    evaluation R X âŸ¶ evaluation R Y â‹™ ModuleCat.restrictScalars (R.map f) where
  app M := ModuleCat.semilinearMapAddEquiv (R.map f) _ _ (M.map f)
  naturality := fun M N Ï† => by
    ext x
    exact (congr_hom (Ï†.hom.naturality f) x).symm

variable {R}

lemma restriction_app_apply {X Y : Cáµ’áµ–} (f : X âŸ¶ Y) (M : PresheafOfModules R) (x : M.obj X) :
    (restriction R f).app M x = M.map f x := by
  rfl

lemma restriction_app_id (M : PresheafOfModules R) (X : Cáµ’áµ–) :
    (restriction R (ðŸ™ X)).app M =
      (ModuleCat.restrictScalarsId' (R.map (ðŸ™ X)) (R.map_id X)).inv.app (M.obj X) := by
  ext x
  rw [restriction_app_apply, map_id, id'_apply]
  erw [ModuleCat.restrictScalarsId'_inv_apply]

lemma restriction_app_comp (M : PresheafOfModules R) {X Y Z : Cáµ’áµ–} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    (restriction R (f â‰« g)).app M =
      (restriction R f).app M â‰«
        (ModuleCat.restrictScalars (R.map f)).map ((restriction R g).app M) â‰«
        (ModuleCat.restrictScalarsComp' _ _ _ (R.map_comp f g)).inv.app (M.obj Z) := by
  ext x
  dsimp
  erw [ModuleCat.restrictScalarsComp'_inv_apply, M.restriction_app_apply f,
    M.restriction_app_apply g, M.restriction_app_apply (f â‰« g), map_comp]
  rfl

end PresheafOfModules

variable (R) in
/-- This structure contains the data and axioms in order to
produce a `PresheafOfModules R` from a collection of types
equipped with module structures over the various rings `R.obj X`.
(See the constructor `PresheafOfModules.mk'`.) -/
structure CorePresheafOfModules where
  /-- the datum of a type for each object in `Cáµ’áµ–` -/
  obj (X : Cáµ’áµ–) : Type v
  /-- the abelian group structure on the types `obj X` -/
  addCommGroup (X : Cáµ’áµ–) : AddCommGroup (obj X) := by infer_instance
  /-- the module structure on the types `obj X` over the various rings `R.obj X` -/
  module (X : Cáµ’áµ–) : Module (R.obj X) (obj X) := by infer_instance
  /-- the semi-linear restriction maps -/
  map {X Y : Cáµ’áµ–} (f : X âŸ¶ Y) : obj X â†’â‚›â‚—[R.map f] obj Y
  /-- `map` is compatible with the identities -/
  map_id (X : Cáµ’áµ–) (x : obj X) : map (ðŸ™ X) x = x := by aesop_cat
  /-- `map` is compatible with the composition -/
  map_comp {X Y Z : Cáµ’áµ–} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (x : obj X) :
    map (f â‰« g) x = map g (map f x) := by aesop_cat

-- this example is meant to test automation: the axioms for `CorePresheafOfModules` are
-- automatically found if we use the data from `M : PresheafOfModules R`
example (M : PresheafOfModules R) : CorePresheafOfModules R where
  obj X := M.obj X
  map f := M.map f

namespace CorePresheafOfModules

attribute [instance] addCommGroup module
attribute [simp] map_id map_comp

variable (M : CorePresheafOfModules R)

/-- The presheaf of abelian groups attached to a `CorePresheafOfModules R`. -/
@[simps]
def presheaf : Cáµ’áµ– â¥¤ AddCommGroupCat.{v} where
  obj X := AddCommGroupCat.of (M.obj X)
  map f := AddCommGroupCat.ofHom (M.map f).toAddMonoidHom

instance (X : Cáµ’áµ–) : Module (R.obj X) (M.presheaf.obj X) := M.module X

end CorePresheafOfModules

namespace PresheafOfModules

/-- Constructor for `PresheafOfModules R` based on a collection of types
equipped with module structures over the various rings `R.obj X`, see
the structure `CorePresheafOfModules`. -/
def mk' (M : CorePresheafOfModules R) : PresheafOfModules R where
  presheaf := M.presheaf

@[simp]
lemma mk'_obj (M : CorePresheafOfModules R) (X : Cáµ’áµ–) :
    (mk' M).obj X = ModuleCat.of _ (M.obj X) := rfl

end PresheafOfModules

variable (R) in
/-- This structure contains the data and axioms in order to
produce a `PresheafOfModules R` from a collection of objects
of type `ModuleCat (R.obj X)` for all `X`, and restriction
maps expressed as linear maps to restriction of scalars.
(See the constructor `PresheafOfModules.mk''`.) -/
structure BundledCorePresheafOfModules where
  /-- the datum of a `ModuleCat (R.obj X)` for each object in `Cáµ’áµ–` -/
  obj (X : Cáµ’áµ–) : ModuleCat.{v} (R.obj X)
  /-- the restriction maps as linear maps to restriction of scalars -/
  map {X Y : Cáµ’áµ–} (f : X âŸ¶ Y) : obj X âŸ¶ (ModuleCat.restrictScalars (R.map f)).obj (obj Y)
  /-- `map` is compatible with the identities -/
  map_id (X : Cáµ’áµ–) :
    map (ðŸ™ X) = (ModuleCat.restrictScalarsId' (R.map (ðŸ™ X)) (R.map_id X)).inv.app (obj X)
  /-- `map` is compatible with the composition -/
  map_comp {X Y Z : Cáµ’áµ–} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    map (f â‰« g) = map f â‰« (ModuleCat.restrictScalars (R.map f)).map (map g) â‰«
      (ModuleCat.restrictScalarsComp' (R.map f) (R.map g) (R.map (f â‰« g))
        (R.map_comp f g)).inv.app (obj Z)

namespace BundledCorePresheafOfModules

variable (M : BundledCorePresheafOfModules R)

/-- The obvious map `BundledCorePresheafOfModules R â†’ CorePresheafOfModules R`. -/
noncomputable def toCorePresheafOfModules : CorePresheafOfModules R where
  obj X := (M.obj X).carrier
  map {X Y} f := (ModuleCat.semilinearMapAddEquiv (R.map f) (M.obj X) (M.obj Y)).symm (M.map f)
  map_id X x := by
    dsimp
    erw [M.map_id, ModuleCat.restrictScalarsId'_inv_apply]
  map_comp {X Y Z} f g x := by
    dsimp
    erw [M.map_comp, ModuleCat.restrictScalarsComp'_inv_apply]
    rfl

end BundledCorePresheafOfModules

namespace PresheafOfModules

/-- Constructor for `PresheafOfModules R` based on a collection of objects
of type `ModuleCat (R.obj X)` for all `X`, and restriction maps expressed
as linear maps to restriction of scalars, see
the structure `BundledCorePresheafOfModules`. -/
noncomputable def mk'' (M : BundledCorePresheafOfModules R) : PresheafOfModules R :=
  mk' M.toCorePresheafOfModules

@[simp]
lemma mk''_obj (M : BundledCorePresheafOfModules R) (X : Cáµ’áµ–) :
    (mk'' M).obj X = (M.obj X).carrier := rfl

@[simp]
lemma restriction_app_mk'' (M : BundledCorePresheafOfModules R) {X Y : Cáµ’áµ–} (f : X âŸ¶ Y) :
    (restriction R f).app (mk'' M) = M.map f := rfl

@[simp]
lemma mk''_presheaf_map_apply (M : BundledCorePresheafOfModules R) {X Y : Cáµ’áµ–} (f : X âŸ¶ Y)
    (x : M.obj X) :
    (mk'' M).presheaf.map f x = M.map f x := rfl

namespace Hom

variable {P Q : PresheafOfModules R}
  (app : âˆ€ X, P.obj X â†’â‚—[R.obj X] Q.obj X)
  (naturality : âˆ€ â¦ƒX Y : Cáµ’áµ–â¦„ (f : X âŸ¶ Y),
    (restriction R f).app P â‰« (ModuleCat.restrictScalars (R.map f)).map (app Y) =
      ModuleCat.ofHom (app X) â‰« (restriction R f).app Q)

/-- A constructor for morphisms in `PresheafOfModules R` that is based on the data
of a family of linear maps over the various rings `R.obj X`, and for which the
naturality condition is stated using the restriction of scalars. -/
def mk'' : P âŸ¶ Q where
  hom :=
    { app := fun X => (app X).toAddMonoidHom
      naturality := fun X Y f => by
        ext x
        exact congr_hom (naturality f) x }
  map_smul X := (app X).map_smul

@[simp]
lemma mk''_app : (mk'' app naturality).app = app := rfl

end Hom

variable {R' : Cáµ’áµ– â¥¤ RingCat.{u}} (M' M'' : PresheafOfModules.{v} R') (Ïˆ : M' âŸ¶ M'') (Ï† : R âŸ¶ R')

@[simps]
noncomputable def restrictScalarsBundledCore : BundledCorePresheafOfModules.{v} R where
  obj X := (ModuleCat.restrictScalars (Ï†.app X)).obj (M'.obj X)
  map {X Y} f := (ModuleCat.restrictScalars (Ï†.app X)).map ((restriction R' f).app M') â‰«
    (ModuleCat.restrictScalarsComp' _ _ _ (Ï†.naturality f)).inv.app _
  map_id X := by
    ext x
    dsimp
    erw [ModuleCat.restrictScalarsId'_inv_apply, ModuleCat.restrictScalarsComp'_inv_apply,
      restriction_app_apply, map_id]
    rfl
  map_comp f g := by
    ext x
    dsimp
    erw [ModuleCat.restrictScalarsComp'_inv_apply, ModuleCat.restrictScalarsComp'_inv_apply,
      ModuleCat.restrictScalarsComp'_inv_apply, ModuleCat.restrictScalarsComp'_inv_apply,
      restriction_app_apply f, restriction_app_apply g, restriction_app_apply, map_comp]
    rfl

noncomputable abbrev restrictScalars : PresheafOfModules.{v} R :=
  mk'' (restrictScalarsBundledCore M' Ï†)

noncomputable def restrictScalarsPresheafIso :
    (M'.restrictScalars Ï†).presheaf â‰… M'.presheaf :=
  NatIso.ofComponents (fun X => Iso.refl _) (fun {X Y} f => by
    ext x
    dsimp
    erw [ModuleCat.restrictScalarsComp'_inv_apply]
    rfl)

variable {M' M''}

noncomputable def restrictScalarsMap : M'.restrictScalars Ï† âŸ¶ M''.restrictScalars Ï† :=
  Hom.mk'' (fun X => (ModuleCat.restrictScalars (Ï†.app X)).map (Ïˆ.app X)) (fun X Y f => by
    ext x
    dsimp
    erw [ModuleCat.coe_comp, ModuleCat.coe_comp]
    dsimp
    erw [ModuleCat.restrictScalarsComp'_inv_apply,
      ModuleCat.restrictScalars.map_apply,
      ModuleCat.restrictScalarsComp'_inv_apply,
      ModuleCat.restrictScalars.map_apply]
    rw [ModuleCat.ofHom_apply, ModuleCat.restrictScalars.map_apply]
    erw [restriction_app_apply f, NatTrans.naturality_apply Ïˆ.hom f,
      restriction_app_apply])

@[simp]
lemma restrictScalarsMap_app_apply {X : Cáµ’áµ–} (x : M'.obj X) :
    (restrictScalarsMap Ïˆ Ï†).app X x = Ïˆ.app X x := rfl

@[simps]
noncomputable def restrictScalarsFunctor :
    PresheafOfModules.{v} R' â¥¤ PresheafOfModules.{v} R where
  obj M' := M'.restrictScalars Ï†
  map Ïˆ := restrictScalarsMap Ïˆ Ï†

end PresheafOfModules
