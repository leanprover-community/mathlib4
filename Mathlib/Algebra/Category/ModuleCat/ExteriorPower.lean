/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Category.ModuleCat.ChangeOfRings
import Mathlib.LinearAlgebra.ExteriorAlgebra.OfAlternating

/-!
# The exterior power operations on modules

-/

universe w v u

namespace Submodule

variable {R M N : Type*} [Semiring R] [AddCommMonoid M] [Module R M]
  [AddCommMonoid N] [Module R N]

lemma linearMap_eq_zero_iff_of_span_eq_top (f : M ‚Üí‚Çó[R] N)
    {S : Set M} (hM : span R S = ‚ä§) :
    f = 0 ‚Üî ‚àÄ (s : S), f s = 0 := by
  rw [‚Üê LinearMap.ker_eq_top, ‚Üê top_le_iff, ‚Üê hM, span_le]
  aesop

lemma linearMap_eq_zero_iff_of_eq_span {V : Submodule R M} (f : V ‚Üí‚Çó[R] N)
    {S : Set M} (hV : V = span R S) :
    f = 0 ‚Üî ‚àÄ (s : S), f ‚ü®s, by simpa only [hV] using subset_span (by simp)‚ü© = 0 := by
  constructor
  ¬∑ rintro rfl s
    rfl
  ¬∑ intro hf
    rw [linearMap_eq_zero_iff_of_span_eq_top (S := (V.subtype ‚Åª¬π' S))]
    ¬∑ subst hV
      rintro ‚ü®‚ü®s, _‚ü©, hs‚ü©
      exact hf ‚ü®s, hs‚ü©
    ¬∑ subst hV
      simp

end Submodule

namespace ExteriorAlgebra

variable (R : Type u) [CommRing R] (n : ‚Ñï) (M : Type v) [AddCommGroup M] [Module R M]

def exteriorProduct : AlternatingMap R M (exteriorPower R n M) (Fin n) where
  toFun m := ‚ü®ŒπMulti R n m, ŒπMulti_range _ _ (by simp)‚ü©
  map_add' m i x y := Subtype.ext (by simp)
  map_smul' m i c x := Subtype.ext (by simp)
  map_eq_zero_of_eq' v i j hij hij' :=
    Subtype.ext ((ŒπMulti R (M := M) n).map_eq_zero_of_eq v hij hij')

namespace exteriorPower

variable {R n M} {N : Type w} [AddCommGroup N] [Module R N]
  (œÜ : AlternatingMap R M N (Fin n))

def desc : exteriorPower R n M ‚Üí‚Çó[R] N :=
  LinearMap.comp (liftAlternating
    (Function.update (fun n ‚Ü¶ (0 : M [‚ãÄ^Fin n]‚Üí‚Çó[R] N)) n œÜ)) (Submodule.subtype _)

@[simp]
lemma desc_exteriorProduct (m : Fin n ‚Üí M) :
    desc œÜ (exteriorProduct R n M m) = œÜ m := by
  dsimp [desc]
  erw [liftAlternating_apply_ŒπMulti]
  simp

lemma linearMap_eq_zero_iff (f : exteriorPower R n M ‚Üí‚Çó[R] N) :
    f = 0 ‚Üî ‚àÄ (m : Fin n ‚Üí M), f (exteriorProduct R n M m) = 0 := by
  constructor
  ¬∑ rintro rfl _
    rfl
  ¬∑ rw [Submodule.linearMap_eq_zero_iff_of_eq_span f
      (ŒπMulti_span_fixedDegree R (M := M) n).symm]
    rintro hf ‚ü®_, m, rfl‚ü©
    exact hf m

@[ext]
lemma linearMap_ext {f g : exteriorPower R n M ‚Üí‚Çó[R] N}
    (h : ‚àÄ (m : Fin n ‚Üí M), f (exteriorProduct R n M m) = g (exteriorProduct R n M m)) :
    f = g := by
  rw [‚Üê sub_eq_zero, linearMap_eq_zero_iff]
  intro m
  rw [LinearMap.sub_apply, sub_eq_zero]
  exact h m

end exteriorPower

end ExteriorAlgebra

open CategoryTheory

namespace ModuleCat

section

variable {R : Type u} [CommRing R] (M : ModuleCat.{u} R)

def homEquivOfSelf :
    (of R R ‚ü∂ M) ‚âÉ M :=
  (LinearMap.ringLmapEquivSelf R R M).toEquiv

@[simp] lemma homEquivOfSelf_apply (œÜ : of R R ‚ü∂ M) : M.homEquivOfSelf œÜ = œÜ (1 : R) := rfl
@[simp] lemma homEquivOfSelf_symm_apply (m : M) (r : R) : M.homEquivOfSelf.symm m r = r ‚Ä¢ m := rfl

end

section

variable {R : Type u} [CommRing R]

def exteriorPower (M : ModuleCat.{u} R) (n : ‚Ñï) : ModuleCat.{u} R :=
  ModuleCat.of R (‚ãÄ[R]^n M)

def AlternatingMap (M N : ModuleCat.{u} R) (n : ‚Ñï) :=
  M [‚ãÄ^Fin n]‚Üí‚Çó[R] N

instance {M N : ModuleCat.{u} R} {n : ‚Ñï} :
    FunLike (M.AlternatingMap N n) (Fin n ‚Üí M) N :=
  inferInstanceAs (FunLike (M [‚ãÄ^Fin n]‚Üí‚Çó[R] N) (Fin n ‚Üí M) N)

end

namespace AlternatingMap

variable {R : Type u} [CommRing R] {M N N' : ModuleCat.{u} R} {n : ‚Ñï}

@[ext]
lemma ext {œÜ œÜ' : M.AlternatingMap N n} (h : ‚àÄ (m : Fin n ‚Üí M), œÜ m = œÜ' m) : œÜ = œÜ' :=
  DFunLike.coe_injective (by funext; apply h)

@[ext 1100]
lemma ext‚ÇÄ {œÜ œÜ' : M.AlternatingMap N 0} (h : œÜ 0 = œÜ' 0) : œÜ = œÜ' := by
  ext m
  obtain rfl : m = 0 := by funext x; fin_cases x
  exact h

def postcomp (œÜ : M.AlternatingMap N n) (f : N ‚ü∂ N') :
    M.AlternatingMap N' n :=
  f.compAlternatingMap œÜ

@[simp]
lemma postcomp_apply (œÜ : M.AlternatingMap N n) (f : N ‚ü∂ N') (m : Fin n ‚Üí M) :
    œÜ.postcomp f m = f (œÜ m) := rfl

@[simp]
lemma postcomp_id (œÜ : M.AlternatingMap N n) :
    œÜ.postcomp (ùüô _) = œÜ := rfl

@[simp]
lemma postcomp_comp (œÜ : M.AlternatingMap N n)
    (f : N ‚ü∂ N') {N'' : ModuleCat.{u} R} (g : N' ‚ü∂ N'') :
    œÜ.postcomp (f ‚â´ g) = (œÜ.postcomp f).postcomp g := rfl

lemma congr_apply {œÜ œÜ' : M.AlternatingMap N n} (h : œÜ = œÜ') (m : Fin n ‚Üí M) :
    œÜ m = œÜ' m := by
  rw [h]

end AlternatingMap

@[simps]
def alternatingMapFunctor {R : Type u} [CommRing R] (M : ModuleCat.{u} R) (n : ‚Ñï) :
    ModuleCat.{u} R ‚•§ Type u where
  obj N := M.AlternatingMap N n
  map {N N'} f œÜ := œÜ.postcomp f

namespace AlternatingMap

variable {R : Type u} [CommRing R] {M N‚ÇÄ N‚ÇÅ : ModuleCat.{u} R} {n : ‚Ñï}

structure Universal (œÜ : M.AlternatingMap N‚ÇÄ n) where
  desc {N : ModuleCat.{u} R} (œà : M.AlternatingMap N n) : N‚ÇÄ ‚ü∂ N
  fac {N : ModuleCat.{u} R} (œà : M.AlternatingMap N n) : œÜ.postcomp (desc œà) = œà
  postcomp_injective {N : ModuleCat.{u} R} {f g : N‚ÇÄ ‚ü∂ N}
    (h : œÜ.postcomp f = œÜ.postcomp g) : f = g

variable (M n)

def univ (M : ModuleCat.{u} R) (n : ‚Ñï) : M.AlternatingMap (M.exteriorPower n) n :=
  ExteriorAlgebra.exteriorProduct _ _ _

def univUniversal : (univ M n).Universal where
  desc {N} œÜ := ExteriorAlgebra.exteriorPower.desc œÜ
  fac {N} œÜ := by
    ext m
    apply ExteriorAlgebra.exteriorPower.desc_exteriorProduct
  postcomp_injective {N f g} h :=
    ExteriorAlgebra.exteriorPower.linearMap_ext (congr_apply h)

namespace Universal

attribute [simp] fac

variable {M n} {œÜ : M.AlternatingMap N‚ÇÄ n}

lemma hom_ext (hœÜ : œÜ.Universal) {N : ModuleCat.{u} R} {f g : N‚ÇÄ ‚ü∂ N}
    (h : ‚àÄ (m : Fin n ‚Üí M), f (œÜ m) = g (œÜ m)) : f = g :=
  hœÜ.postcomp_injective (by ext; apply h)

variable (hœÜ : œÜ.Universal)

@[simp]
lemma desc_apply {N : ModuleCat.{u} R} (œà : M.AlternatingMap N n) (m : Fin n ‚Üí M) :
    hœÜ.desc œà (œÜ m) = œà m := by
  conv_rhs => rw [‚Üê hœÜ.fac œà]
  rfl

section

variable {œà : M.AlternatingMap N‚ÇÅ n} (hœà : œà.Universal)

def uniq : N‚ÇÄ ‚âÖ N‚ÇÅ where
  hom := hœÜ.desc œà
  inv := hœà.desc œÜ
  hom_inv_id := hœÜ.postcomp_injective (by simp)
  inv_hom_id := hœà.postcomp_injective (by simp)

@[simp] lemma postcomp_uniq_hom : œÜ.postcomp (uniq hœÜ hœà).hom = œà := by simp [uniq]
@[simp] lemma postcomp_uniq_inv : œà.postcomp (uniq hœÜ hœà).inv = œÜ := by simp [uniq]

end

def iso : M.exteriorPower n ‚âÖ N‚ÇÄ :=
  (univUniversal M n).uniq hœÜ

@[simp] lemma postcomp_iso_hom : (univ M n).postcomp hœÜ.iso.hom = œÜ := by simp [iso]
@[simp] lemma postcomp_iso_inv : œÜ.postcomp hœÜ.iso.inv = univ M n := by simp [iso]

end Universal

section

variable {M n} (r : (M.alternatingMapFunctor n).CorepresentableBy N‚ÇÄ)

def ofCorepresentableBy : M.AlternatingMap N‚ÇÄ n := r.homEquiv (ùüô _)

def universalOfCorepresentableBy : (ofCorepresentableBy r).Universal where
  desc œà := r.homEquiv.symm œà
  fac œà := by
    obtain ‚ü®œÜ, rfl‚ü© := r.homEquiv.surjective œà
    dsimp [ofCorepresentableBy]
    simp only [Equiv.symm_apply_apply]
    erw [r.homEquiv_eq œÜ]
    rfl
  postcomp_injective {N f g} h := by
    apply r.homEquiv.injective
    rw [r.homEquiv_eq f, r.homEquiv_eq g]
    exact h

end

end AlternatingMap

section

variable {R : Type u} [CommRing R] (M N : ModuleCat.{u} R)

def equiv‚ÇÄ : M.AlternatingMap N 0 ‚âÉ N :=
  AlternatingMap.constLinearEquivOfIsEmpty.toEquiv.symm

def corepresentableBy‚ÇÄ :
    (M.alternatingMapFunctor 0).CorepresentableBy (ModuleCat.of R R) where
  homEquiv {N} := N.homEquivOfSelf.trans (equiv‚ÇÄ M N).symm
  homEquiv_comp _ _ := rfl

def exteriorPower‚ÇÄIso : M.exteriorPower 0 ‚âÖ of R R :=
  (AlternatingMap.universalOfCorepresentableBy M.corepresentableBy‚ÇÄ).iso

@[simp]
lemma _root_.Function.update_apply_of_subsingleton
    {M : Type*} (œÜ : Fin 1 ‚Üí M) (x : Fin 1) (m : M) (i : Fin 1) :
    Function.update œÜ x m i = m := by
  obtain rfl := Subsingleton.elim x i
  simp

def equiv‚ÇÅ : M.AlternatingMap N 1 ‚âÉ (M ‚ü∂ N) where
  toFun œÜ :=
    { toFun := fun x ‚Ü¶ œÜ (fun _ ‚Ü¶ x)
      map_add' := fun x y ‚Ü¶ by convert œÜ.map_add 0 0 x y <;> simp
      map_smul' := fun r x ‚Ü¶ by convert œÜ.map_smul 0 0 r x <;> simp }
  invFun f :=
    { toFun := fun x ‚Ü¶ f (x 0)
      map_add' := fun _ i x y ‚Ü¶ by fin_cases i; convert f.map_add x y <;> simp
      map_smul' := fun _ i r x ‚Ü¶ by fin_cases i; convert f.map_smul r x <;> simp
      map_eq_zero_of_eq' := fun _ i j ‚Ü¶ by fin_cases i; fin_cases j; tauto }
  left_inv œÜ := by
    ext m
    obtain ‚ü®x, rfl‚ü© : ‚àÉ x, m = fun _ ‚Ü¶ x := ‚ü®m 0, by ext i; fin_cases i; rfl‚ü©
    rfl
  right_inv f := rfl

def corepresentableBy‚ÇÅ :
    (M.alternatingMapFunctor 1).CorepresentableBy M where
  homEquiv {N} := (equiv‚ÇÅ M N).symm
  homEquiv_comp _ _ := rfl

def exteriorPower‚ÇÅIso : M.exteriorPower 1 ‚âÖ M :=
  (AlternatingMap.universalOfCorepresentableBy M.corepresentableBy‚ÇÅ).iso

end

namespace exteriorPower

section

variable {R : Type u} [CommRing R] {M : ModuleCat.{u} R}

def desc {n : ‚Ñï} {N : ModuleCat.{u} R} (œÜ : M.AlternatingMap N n) :
    M.exteriorPower n ‚ü∂ N :=
  (AlternatingMap.univUniversal M n).desc œÜ

def mk {n : ‚Ñï} (m : Fin n ‚Üí M) : M.exteriorPower n :=
  AlternatingMap.univ M n m

@[simp]
lemma desc_mk {n : ‚Ñï} {N : ModuleCat.{u} R} (œÜ : M [‚ãÄ^Fin n]‚Üí‚Çó[R] N)
    (m : Fin n ‚Üí M) :
    desc œÜ (mk m) = œÜ m := by
  simp [desc, mk]

@[ext]
lemma hom_ext {n : ‚Ñï} {N : ModuleCat.{u} R} {f g : M.exteriorPower n ‚ü∂ N}
    (h : ‚àÄ (m : Fin n ‚Üí M), f (mk m) = g (mk m)) :
    f = g :=
  (AlternatingMap.univUniversal M n).hom_ext h

def map {N : ModuleCat.{u} R} (f : M ‚ü∂ N) (n : ‚Ñï) : M.exteriorPower n ‚ü∂ N.exteriorPower n :=
  desc (AlternatingMap.compLinearMap (AlternatingMap.univ N n) f)

@[simp]
lemma map_mk {N : ModuleCat.{u} R} (f : M ‚ü∂ N) {n : ‚Ñï} (m : Fin n ‚Üí M) :
    map f n (mk m) = mk (Function.comp f m) := by
  simp only [map, desc_mk, AlternatingMap.compLinearMap_apply]
  rfl

variable (M R)

@[simps]
def functor (n : ‚Ñï): ModuleCat.{u} R ‚•§ ModuleCat.{u} R where
  obj M := M.exteriorPower n
  map f := map f n

end

section ChangeOfRings

variable {R S : Type u} [CommRing R] [CommRing S] (f : R ‚Üí+* S)

noncomputable def fromRestrictScalarsObjExteriorPower (M : ModuleCat.{u} S) (n : ‚Ñï) :
    ((restrictScalars f).obj M).exteriorPower n ‚ü∂
      (restrictScalars f).obj (M.exteriorPower n) :=
  desc
    { toFun := fun m ‚Ü¶ mk m
      map_add' := fun m i x y ‚Ü¶ (AlternatingMap.univ M n).map_add m i x y
      map_smul' := fun m i r x ‚Ü¶ (AlternatingMap.univ M n).map_smul m i (f r) x
      map_eq_zero_of_eq' := fun m _ _ hm hij ‚Ü¶
        (AlternatingMap.univ M n).map_eq_zero_of_eq m hm hij }

@[simp]
lemma fromRestrictScalarsObjExteriorPower_mk (M : ModuleCat.{u} S) (n : ‚Ñï)
    (m : Fin n ‚Üí M) :
    fromRestrictScalarsObjExteriorPower f M n (mk m) = mk m := by
  apply desc_mk

@[simps]
noncomputable def restrictScalarsCompFunctorNatTrans (n : ‚Ñï) :
    restrictScalars f ‚ãô functor R n ‚ü∂ functor S n ‚ãô restrictScalars f where
  app M := fromRestrictScalarsObjExteriorPower f M n

end ChangeOfRings

end exteriorPower

end ModuleCat
