/-
Copyright (c) 2023 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import Mathlib.Algebra.Category.ModuleCat.Abelian
import Mathlib.Algebra.Category.ModuleCat.ChangeOfRings
import Mathlib.Algebra.Category.GroupCat.Abelian
import Mathlib.Algebra.Category.GroupCat.Injective
import Mathlib.Algebra.Category.GroupCat.ZModuleEquivalence
import Mathlib.CategoryTheory.Abelian.Transfer

/-!
# Category of $R$-modules has enough injectives

we lift enough injectives of abelian groups to arbitrary $R$-modules by adjoint functors
`restrictScalars ‚ä£ coextendScalars`

## Implementation notes
This file is not part of `Algebra/Module/Injective.lean` to prevent import loop: enough-injectives
of abelian groups needs `Algebra/Module/Injective.lean` and this file needs enough-injectives of
abelian groups.
-/

open CategoryTheory

universe u v
variable (R : Type u) [Ring R]

instance : EnoughInjectives (ModuleCat.{max v u} R) :=
  EnoughInjectives.of_adjunction
    (L := forget‚ÇÇ (ModuleCat.{max v u} R) AddCommGroupCat.{max v u})
    (R := (forget‚ÇÇ (ModuleCat.{max v u} ‚Ñ§) AddCommGroupCat.{max v u}).asEquivalence.inverse ‚ãô
      ModuleCat.coextendScalars (algebraMap ‚Ñ§ R)) <|
    let iso : ModuleCat.restrictScalars.{max v u} (algebraMap ‚Ñ§ R) ‚ãô
      (forget‚ÇÇ (ModuleCat.{max v u} ‚Ñ§) AddCommGroupCat.{max v u}).asEquivalence.functor ‚âÖ
      forget‚ÇÇ (ModuleCat.{max v u} R) AddCommGroupCat.{max v u} :=
    { hom :=
      { app := fun _ => ùüô _
        naturality := by aesop_cat }
      inv :=
      { app := fun _ => ùüô _
        naturality := by aesop_cat }
      hom_inv_id := by aesop_cat
      inv_hom_id := by aesop_cat }
    (((ModuleCat.restrictCoextendScalarsAdj.{max v u} (algebraMap ‚Ñ§ R))).comp <|
      (forget‚ÇÇ (ModuleCat ‚Ñ§) AddCommGroupCat).asEquivalence.toAdjunction).ofNatIsoLeft iso
