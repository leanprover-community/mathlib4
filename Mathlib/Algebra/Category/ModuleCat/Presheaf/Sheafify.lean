/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Category.ModuleCat.Sheaf.ChangeOfRings
import Mathlib.CategoryTheory.Sites.LocallySurjective

/-!
# The associated sheaf of a presheaf of modules

In this file, given a presheaf of modules `M‚ÇÄ` over a presheaf of rings `R‚ÇÄ`,
we construct the associated sheaf of `M‚ÇÄ`. More precisely, if `R` is a sheaf of
rings and `Œ± : R‚ÇÄ ‚ü∂ R.val` is locally bijective, and `A` is the sheafification
of the underlying presheaf of abelian groups of `M‚ÇÄ`, i.e. we have a locally bijective
map `œÜ : M‚ÇÄ.presheaf ‚ü∂ A.val`, then we endow `A` with the structure of a
sheaf of modules over `R`: this is `PresheafOfModules.sheafify Œ± œÜ`.

In many applications, the morphism `Œ±` shall be the identity, but this more
general construction allows the sheafification of both the presheaf of rings
and the presheaf of modules.

-/

universe w v v‚ÇÅ u‚ÇÅ u

open CategoryTheory Functor

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {J : GrothendieckTopology C}

namespace CategoryTheory

namespace Presieve.FamilyOfElements

section smul

variable {R : C·µí·µñ ‚•§ RingCat.{u}} {M : PresheafOfModules.{v} R} {X : C} {P : Presieve X}
  (r : FamilyOfElements (R ‚ãô forget _) P) (m : FamilyOfElements (M.presheaf ‚ãô forget _) P)

/-- The scalar multiplication of family of elements of a presheaf of modules `M` over `R`
by a family of elements of `R`. -/
def smul : FamilyOfElements (M.presheaf ‚ãô forget _) P := fun Y f hf =>
  HSMul.hSMul (Œ± := R.obj (Opposite.op Y)) (Œ≤ := M.obj (Opposite.op Y)) (r f hf) (m f hf)

end smul

section

variable {R‚ÇÄ R : C·µí·µñ ‚•§ RingCat.{u}} (Œ± : R‚ÇÄ ‚ü∂ R) [Presheaf.IsLocallyInjective J Œ±]
  {M‚ÇÄ : PresheafOfModules.{v} R‚ÇÄ} {A : C·µí·µñ ‚•§ AddCommGrpCat.{v}} (œÜ : M‚ÇÄ.presheaf ‚ü∂ A)
  [Presheaf.IsLocallyInjective J œÜ] (hA : Presheaf.IsSeparated J A)
  {X : C} (r : R.obj (Opposite.op X)) (m : A.obj (Opposite.op X)) {P : Presieve X}
  (r‚ÇÄ : FamilyOfElements (R‚ÇÄ ‚ãô forget _) P) (m‚ÇÄ : FamilyOfElements (M‚ÇÄ.presheaf ‚ãô forget _) P)
include hA

lemma _root_.PresheafOfModules.Sheafify.app_eq_of_isLocallyInjective
    {Y : C} (r‚ÇÄ r‚ÇÄ' : R‚ÇÄ.obj (Opposite.op Y))
    (m‚ÇÄ m‚ÇÄ' : M‚ÇÄ.obj (Opposite.op Y))
    (hr‚ÇÄ : Œ±.app _ r‚ÇÄ = Œ±.app _ r‚ÇÄ')
    (hm‚ÇÄ : œÜ.app _ m‚ÇÄ = œÜ.app _ m‚ÇÄ') :
    œÜ.app _ (r‚ÇÄ ‚Ä¢ m‚ÇÄ) = œÜ.app _ (r‚ÇÄ' ‚Ä¢ m‚ÇÄ') := by
  apply hA _ (Presheaf.equalizerSieve r‚ÇÄ r‚ÇÄ' ‚äì
      Presheaf.equalizerSieve (F := M‚ÇÄ.presheaf) m‚ÇÄ m‚ÇÄ')
  ¬∑ apply J.intersection_covering
    ¬∑ exact Presheaf.equalizerSieve_mem J Œ± _ _ hr‚ÇÄ
    ¬∑ exact Presheaf.equalizerSieve_mem J œÜ _ _ hm‚ÇÄ
  ¬∑ intro Z g hg
    rw [‚Üê NatTrans.naturality_apply (D := Ab), ‚Üê NatTrans.naturality_apply (D := Ab)]
    erw [M‚ÇÄ.map_smul, M‚ÇÄ.map_smul, hg.1, hg.2]
    rfl

lemma isCompatible_map_smul_aux {Y Z : C} (f : Y ‚ü∂ X) (g : Z ‚ü∂ Y)
    (r‚ÇÄ : R‚ÇÄ.obj (Opposite.op Y)) (r‚ÇÄ' : R‚ÇÄ.obj (Opposite.op Z))
    (m‚ÇÄ : M‚ÇÄ.obj (Opposite.op Y)) (m‚ÇÄ' : M‚ÇÄ.obj (Opposite.op Z))
    (hr‚ÇÄ : Œ±.app _ r‚ÇÄ = R.map f.op r) (hr‚ÇÄ' : Œ±.app _ r‚ÇÄ' = R.map (f.op ‚â´ g.op) r)
    (hm‚ÇÄ : œÜ.app _ m‚ÇÄ = A.map f.op m) (hm‚ÇÄ' : œÜ.app _ m‚ÇÄ' = A.map (f.op ‚â´ g.op) m) :
    œÜ.app _ (M‚ÇÄ.map g.op (r‚ÇÄ ‚Ä¢ m‚ÇÄ)) = œÜ.app _ (r‚ÇÄ' ‚Ä¢ m‚ÇÄ') := by
  rw [‚Üê PresheafOfModules.Sheafify.app_eq_of_isLocallyInjective Œ± œÜ hA (R‚ÇÄ.map g.op r‚ÇÄ) r‚ÇÄ'
    (M‚ÇÄ.map g.op m‚ÇÄ) m‚ÇÄ', M‚ÇÄ.map_smul]
  ¬∑ rw [hr‚ÇÄ', R.map_comp, RingCat.comp_apply, ‚Üê hr‚ÇÄ, ‚Üê RingCat.comp_apply, NatTrans.naturality,
      RingCat.comp_apply]
  ¬∑ rw [hm‚ÇÄ', A.map_comp, AddCommGrpCat.coe_comp, Function.comp_apply, ‚Üê hm‚ÇÄ]
    erw [NatTrans.naturality_apply œÜ]

variable (hr‚ÇÄ : (r‚ÇÄ.map (whiskerRight Œ± (forget _))).IsAmalgamation r)
  (hm‚ÇÄ : (m‚ÇÄ.map (whiskerRight œÜ (forget _))).IsAmalgamation m)

include hr‚ÇÄ hm‚ÇÄ in
lemma isCompatible_map_smul : ((r‚ÇÄ.smul m‚ÇÄ).map (whiskerRight œÜ (forget _))).Compatible := by
  intro Y‚ÇÅ Y‚ÇÇ Z g‚ÇÅ g‚ÇÇ f‚ÇÅ f‚ÇÇ h‚ÇÅ h‚ÇÇ fac
  let a‚ÇÅ := r‚ÇÄ f‚ÇÅ h‚ÇÅ
  let b‚ÇÅ := m‚ÇÄ f‚ÇÅ h‚ÇÅ
  let a‚ÇÇ := r‚ÇÄ f‚ÇÇ h‚ÇÇ
  let b‚ÇÇ := m‚ÇÄ f‚ÇÇ h‚ÇÇ
  let a‚ÇÄ := R‚ÇÄ.map g‚ÇÅ.op a‚ÇÅ
  let b‚ÇÄ := M‚ÇÄ.map g‚ÇÅ.op b‚ÇÅ
  have ha‚ÇÅ : (Œ±.app (Opposite.op Y‚ÇÅ)) a‚ÇÅ = (R.map f‚ÇÅ.op) r := (hr‚ÇÄ f‚ÇÅ h‚ÇÅ).symm
  have ha‚ÇÇ : (Œ±.app (Opposite.op Y‚ÇÇ)) a‚ÇÇ = (R.map f‚ÇÇ.op) r := (hr‚ÇÄ f‚ÇÇ h‚ÇÇ).symm
  have hb‚ÇÅ : (œÜ.app (Opposite.op Y‚ÇÅ)) b‚ÇÅ = (A.map f‚ÇÅ.op) m := (hm‚ÇÄ f‚ÇÅ h‚ÇÅ).symm
  have hb‚ÇÇ : (œÜ.app (Opposite.op Y‚ÇÇ)) b‚ÇÇ = (A.map f‚ÇÇ.op) m := (hm‚ÇÄ f‚ÇÇ h‚ÇÇ).symm
  have ha‚ÇÄ : (Œ±.app (Opposite.op Z)) a‚ÇÄ = (R.map (f‚ÇÅ.op ‚â´ g‚ÇÅ.op)) r := by
    rw [‚Üê RingCat.comp_apply, NatTrans.naturality, RingCat.comp_apply, ha‚ÇÅ, Functor.map_comp,
      RingCat.comp_apply]
  have hb‚ÇÄ : (œÜ.app (Opposite.op Z)) b‚ÇÄ = (A.map (f‚ÇÅ.op ‚â´ g‚ÇÅ.op)) m := by
    dsimp [b‚ÇÄ]
    erw [NatTrans.naturality_apply œÜ, hb‚ÇÅ, Functor.map_comp, ConcreteCategory.comp_apply]
  have ha‚ÇÄ' : (Œ±.app (Opposite.op Z)) a‚ÇÄ = (R.map (f‚ÇÇ.op ‚â´ g‚ÇÇ.op)) r := by
    rw [ha‚ÇÄ, ‚Üê op_comp, fac, op_comp]
  have hb‚ÇÄ' : (œÜ.app (Opposite.op Z)) b‚ÇÄ = (A.map (f‚ÇÇ.op ‚â´ g‚ÇÇ.op)) m := by
    rw [hb‚ÇÄ, ‚Üê op_comp, fac, op_comp]
  dsimp
  erw [‚Üê NatTrans.naturality_apply œÜ, ‚Üê NatTrans.naturality_apply œÜ]
  exact (isCompatible_map_smul_aux Œ± œÜ hA r m f‚ÇÅ g‚ÇÅ a‚ÇÅ a‚ÇÄ b‚ÇÅ b‚ÇÄ ha‚ÇÅ ha‚ÇÄ hb‚ÇÅ hb‚ÇÄ).trans
    (isCompatible_map_smul_aux Œ± œÜ hA r m f‚ÇÇ g‚ÇÇ a‚ÇÇ a‚ÇÄ b‚ÇÇ b‚ÇÄ ha‚ÇÇ ha‚ÇÄ' hb‚ÇÇ hb‚ÇÄ').symm

end

end Presieve.FamilyOfElements

end CategoryTheory

variable {R‚ÇÄ : C·µí·µñ ‚•§ RingCat.{u}} {R : Sheaf J RingCat.{u}} (Œ± : R‚ÇÄ ‚ü∂ R.val)
  [Presheaf.IsLocallyInjective J Œ±] [Presheaf.IsLocallySurjective J Œ±]

namespace PresheafOfModules

variable {M‚ÇÄ : PresheafOfModules.{v} R‚ÇÄ} {A : Sheaf J AddCommGrpCat.{v}}
  (œÜ : M‚ÇÄ.presheaf ‚ü∂ A.val)
  [Presheaf.IsLocallyInjective J œÜ] [Presheaf.IsLocallySurjective J œÜ]

namespace Sheafify

variable {X Y : C·µí·µñ} (œÄ : X ‚ü∂ Y) (r r' : R.val.obj X) (m m' : A.val.obj X)

/-- Assuming `Œ± : R‚ÇÄ ‚ü∂ R.val` is the sheafification map of a presheaf of rings `R‚ÇÄ`
and `œÜ : M‚ÇÄ.presheaf ‚ü∂ A.val` is the sheafification map of the underlying
sheaf of abelian groups of a presheaf of modules `M‚ÇÄ` over `R‚ÇÄ`, then given
`r : R.val.obj X` and `m : A.val.obj X`, this structure contains the data
of `x : A.val.obj X` along with the property which makes `x` a good candidate
for the definition of the scalar multiplication `r ‚Ä¢ m`. -/
structure SMulCandidate where
  /-- The candidate for the scalar product `r ‚Ä¢ m`. -/
  x : A.val.obj X
  h ‚¶ÉY : C·µí·µñ‚¶Ñ (f : X ‚ü∂ Y) (r‚ÇÄ : R‚ÇÄ.obj Y) (hr‚ÇÄ : Œ±.app Y r‚ÇÄ = R.val.map f r)
    (m‚ÇÄ : M‚ÇÄ.obj Y) (hm‚ÇÄ : œÜ.app Y m‚ÇÄ = A.val.map f m) : A.val.map f x = œÜ.app Y (r‚ÇÄ ‚Ä¢ m‚ÇÄ)

/-- Constructor for `SMulCandidate`. -/
def SMulCandidate.mk' (S : Sieve X.unop) (hS : S ‚àà J X.unop)
    (r‚ÇÄ : Presieve.FamilyOfElements (R‚ÇÄ ‚ãô forget _) S.arrows)
    (m‚ÇÄ : Presieve.FamilyOfElements (M‚ÇÄ.presheaf ‚ãô forget _) S.arrows)
    (hr‚ÇÄ : (r‚ÇÄ.map (whiskerRight Œ± (forget _))).IsAmalgamation r)
    (hm‚ÇÄ : (m‚ÇÄ.map (whiskerRight œÜ (forget _))).IsAmalgamation m)
    (a : A.val.obj X)
    (ha : ((r‚ÇÄ.smul m‚ÇÄ).map (whiskerRight œÜ (forget _))).IsAmalgamation a) :
    SMulCandidate Œ± œÜ r m where
  x := a
  h Y f a‚ÇÄ ha‚ÇÄ b‚ÇÄ hb‚ÇÄ := by
    apply A.isSeparated _ _ (J.pullback_stable f.unop hS)
    rintro Z g hg
    dsimp at hg
    rw [‚Üê ConcreteCategory.comp_apply, ‚Üê A.val.map_comp, ‚Üê NatTrans.naturality_apply (D := Ab)]
    erw [M‚ÇÄ.map_smul] -- Mismatch between `M‚ÇÄ.map` and `M‚ÇÄ.presheaf.map`
    refine (ha _ hg).trans (app_eq_of_isLocallyInjective Œ± œÜ A.isSeparated _ _ _ _ ?_ ?_)
    ¬∑ rw [‚Üê RingCat.comp_apply, NatTrans.naturality, RingCat.comp_apply, ha‚ÇÄ]
      apply (hr‚ÇÄ _ hg).symm.trans
      simp
    ¬∑ erw [NatTrans.naturality_apply œÜ, hb‚ÇÄ]
      apply (hm‚ÇÄ _ hg).symm.trans
      dsimp
      rw [Functor.map_comp]
      rfl

instance : Nonempty (SMulCandidate Œ± œÜ r m) := ‚ü®by
  let S := (Presheaf.imageSieve Œ± r ‚äì Presheaf.imageSieve œÜ m)
  have hS : S ‚àà J _ := by
    apply J.intersection_covering
    all_goals apply Presheaf.imageSieve_mem
  have h‚ÇÅ : S ‚â§ Presheaf.imageSieve Œ± r := fun _ _ h => h.1
  have h‚ÇÇ : S ‚â§ Presheaf.imageSieve œÜ m := fun _ _ h => h.2
  let r‚ÇÄ := (Presieve.FamilyOfElements.localPreimage (whiskerRight Œ± (forget _)) r).restrict h‚ÇÅ
  let m‚ÇÄ := (Presieve.FamilyOfElements.localPreimage (whiskerRight œÜ (forget _)) m).restrict h‚ÇÇ
  have hr‚ÇÄ : (r‚ÇÄ.map (whiskerRight Œ± (forget _))).IsAmalgamation r := by
    rw [Presieve.FamilyOfElements.restrict_map]
    apply Presieve.isAmalgamation_restrict
    apply Presieve.FamilyOfElements.isAmalgamation_map_localPreimage
  have hm‚ÇÄ : (m‚ÇÄ.map (whiskerRight œÜ (forget _))).IsAmalgamation m := by
    rw [Presieve.FamilyOfElements.restrict_map]
    apply Presieve.isAmalgamation_restrict
    apply Presieve.FamilyOfElements.isAmalgamation_map_localPreimage
  exact SMulCandidate.mk' Œ± œÜ r m S hS r‚ÇÄ m‚ÇÄ hr‚ÇÄ hm‚ÇÄ _ (Presieve.IsSheafFor.isAmalgamation
    (((sheafCompose J (forget _)).obj A).2.isSheafFor S hS)
    (Presieve.FamilyOfElements.isCompatible_map_smul Œ± œÜ A.isSeparated r m r‚ÇÄ m‚ÇÄ hr‚ÇÄ hm‚ÇÄ))‚ü©

instance : Subsingleton (SMulCandidate Œ± œÜ r m) where
  allEq := by
    rintro ‚ü®x‚ÇÅ, h‚ÇÅ‚ü© ‚ü®x‚ÇÇ, h‚ÇÇ‚ü©
    simp only [SMulCandidate.mk.injEq]
    let S := (Presheaf.imageSieve Œ± r ‚äì Presheaf.imageSieve œÜ m)
    have hS : S ‚àà J _ := by
      apply J.intersection_covering
      all_goals apply Presheaf.imageSieve_mem
    apply A.isSeparated _ _ hS
    intro Y f ‚ü®‚ü®r‚ÇÄ, hr‚ÇÄ‚ü©, ‚ü®m‚ÇÄ, hm‚ÇÄ‚ü©‚ü©
    rw [h‚ÇÅ f.op r‚ÇÄ hr‚ÇÄ m‚ÇÄ hm‚ÇÄ, h‚ÇÇ f.op r‚ÇÄ hr‚ÇÄ m‚ÇÄ hm‚ÇÄ]

noncomputable instance : Unique (SMulCandidate Œ± œÜ r m) :=
  uniqueOfSubsingleton (Nonempty.some inferInstance)

/-- The (unique) element in `SMulCandidate Œ± œÜ r m`. -/
noncomputable def smulCandidate : SMulCandidate Œ± œÜ r m := default

/-- The scalar multiplication on the sheafification of a presheaf of modules. -/
noncomputable def smul : A.val.obj X := (smulCandidate Œ± œÜ r m).x

lemma map_smul_eq {Y : C·µí·µñ} (f : X ‚ü∂ Y) (r‚ÇÄ : R‚ÇÄ.obj Y) (hr‚ÇÄ : Œ±.app Y r‚ÇÄ = R.val.map f r)
    (m‚ÇÄ : M‚ÇÄ.obj Y) (hm‚ÇÄ : œÜ.app Y m‚ÇÄ = A.val.map f m) :
    A.val.map f (smul Œ± œÜ r m) = œÜ.app Y (r‚ÇÄ ‚Ä¢ m‚ÇÄ) :=
  (smulCandidate Œ± œÜ r m).h f r‚ÇÄ hr‚ÇÄ m‚ÇÄ hm‚ÇÄ

protected lemma one_smul : smul Œ± œÜ 1 m = m := by
  apply A.isSeparated _ _ (Presheaf.imageSieve_mem J œÜ m)
  rintro Y f ‚ü®m‚ÇÄ, hm‚ÇÄ‚ü©
  rw [‚Üê hm‚ÇÄ, map_smul_eq Œ± œÜ 1 m f.op 1 (by simp) m‚ÇÄ hm‚ÇÄ, one_smul]

protected lemma zero_smul : smul Œ± œÜ 0 m = 0 := by
  apply A.isSeparated _ _ (Presheaf.imageSieve_mem J œÜ m)
  rintro Y f ‚ü®m‚ÇÄ, hm‚ÇÄ‚ü©
  rw [map_smul_eq Œ± œÜ 0 m f.op 0 (by simp) m‚ÇÄ hm‚ÇÄ, zero_smul, map_zero,
    (A.val.map f.op).hom.map_zero]

protected lemma smul_zero : smul Œ± œÜ r 0 = 0 := by
  apply A.isSeparated _ _ (Presheaf.imageSieve_mem J Œ± r)
  rintro Y f ‚ü®r‚ÇÄ, hr‚ÇÄ‚ü©
  rw [(A.val.map f.op).hom.map_zero, map_smul_eq Œ± œÜ r 0 f.op r‚ÇÄ hr‚ÇÄ 0 (by simp),
    smul_zero, map_zero]

protected lemma smul_add : smul Œ± œÜ r (m + m') = smul Œ± œÜ r m + smul Œ± œÜ r m' := by
  let S := Presheaf.imageSieve Œ± r ‚äì Presheaf.imageSieve œÜ m ‚äì Presheaf.imageSieve œÜ m'
  have hS : S ‚àà J X.unop := by
    refine J.intersection_covering (J.intersection_covering ?_ ?_) ?_
    all_goals apply Presheaf.imageSieve_mem
  apply A.isSeparated _ _ hS
  rintro Y f ‚ü®‚ü®‚ü®r‚ÇÄ, hr‚ÇÄ‚ü©, ‚ü®m‚ÇÄ : M‚ÇÄ.obj _, hm‚ÇÄ : (œÜ.app _) _ = _‚ü©‚ü©,
    ‚ü®m‚ÇÄ' : M‚ÇÄ.obj _, hm‚ÇÄ' : (œÜ.app _) _ = _‚ü©‚ü©
  rw [(A.val.map f.op).hom.map_add, map_smul_eq Œ± œÜ r m f.op r‚ÇÄ hr‚ÇÄ m‚ÇÄ hm‚ÇÄ,
    map_smul_eq Œ± œÜ r m' f.op r‚ÇÄ hr‚ÇÄ m‚ÇÄ' hm‚ÇÄ',
    map_smul_eq Œ± œÜ r (m + m') f.op r‚ÇÄ hr‚ÇÄ (m‚ÇÄ + m‚ÇÄ')
      (by rw [_root_.map_add, _root_.map_add, hm‚ÇÄ, hm‚ÇÄ']),
    smul_add, _root_.map_add]

protected lemma add_smul : smul Œ± œÜ (r + r') m = smul Œ± œÜ r m + smul Œ± œÜ r' m := by
  let S := Presheaf.imageSieve Œ± r ‚äì Presheaf.imageSieve Œ± r' ‚äì Presheaf.imageSieve œÜ m
  have hS : S ‚àà J X.unop := by
    refine J.intersection_covering (J.intersection_covering ?_ ?_) ?_
    all_goals apply Presheaf.imageSieve_mem
  apply A.isSeparated _ _ hS
  rintro Y f ‚ü®‚ü®‚ü®r‚ÇÄ : R‚ÇÄ.obj _, (hr‚ÇÄ : (Œ±.app (Opposite.op Y)) r‚ÇÄ = (R.val.map f.op) r)‚ü©,
    ‚ü®r‚ÇÄ' : R‚ÇÄ.obj _, (hr‚ÇÄ' : (Œ±.app (Opposite.op Y)) r‚ÇÄ' = (R.val.map f.op) r')‚ü©‚ü©, ‚ü®m‚ÇÄ, hm‚ÇÄ‚ü©‚ü©
  rw [(A.val.map f.op).hom.map_add, map_smul_eq Œ± œÜ r m f.op r‚ÇÄ hr‚ÇÄ m‚ÇÄ hm‚ÇÄ,
    map_smul_eq Œ± œÜ r' m f.op r‚ÇÄ' hr‚ÇÄ' m‚ÇÄ hm‚ÇÄ,
    map_smul_eq Œ± œÜ (r + r') m f.op (r‚ÇÄ + r‚ÇÄ') (by rw [_root_.map_add, _root_.map_add, hr‚ÇÄ, hr‚ÇÄ'])
      m‚ÇÄ hm‚ÇÄ, add_smul, _root_.map_add]

protected lemma mul_smul : smul Œ± œÜ (r * r') m = smul Œ± œÜ r (smul Œ± œÜ r' m) := by
  let S := Presheaf.imageSieve Œ± r ‚äì Presheaf.imageSieve Œ± r' ‚äì Presheaf.imageSieve œÜ m
  have hS : S ‚àà J X.unop := by
    refine J.intersection_covering (J.intersection_covering ?_ ?_) ?_
    all_goals apply Presheaf.imageSieve_mem
  apply A.isSeparated _ _ hS
  rintro Y f ‚ü®‚ü®‚ü®r‚ÇÄ : R‚ÇÄ.obj _, (hr‚ÇÄ : (Œ±.app (Opposite.op Y)) r‚ÇÄ = (R.val.map f.op) r)‚ü©,
    ‚ü®r‚ÇÄ' : R‚ÇÄ.obj _, (hr‚ÇÄ' : (Œ±.app (Opposite.op Y)) r‚ÇÄ' = (R.val.map f.op) r')‚ü©‚ü©,
    ‚ü®m‚ÇÄ : M‚ÇÄ.obj _, hm‚ÇÄ‚ü©‚ü©
  rw [map_smul_eq Œ± œÜ (r * r') m f.op (r‚ÇÄ * r‚ÇÄ')
    (by rw [map_mul, map_mul, hr‚ÇÄ, hr‚ÇÄ']) m‚ÇÄ hm‚ÇÄ, mul_smul,
    map_smul_eq Œ± œÜ r (smul Œ± œÜ r' m) f.op r‚ÇÄ hr‚ÇÄ (r‚ÇÄ' ‚Ä¢ m‚ÇÄ)
      (map_smul_eq Œ± œÜ r' m f.op r‚ÇÄ' hr‚ÇÄ' m‚ÇÄ hm‚ÇÄ).symm]

variable (X)

/-- The module structure on the sections of the sheafification of the underlying
presheaf of abelian groups of a presheaf of modules. -/
noncomputable def module : Module (R.val.obj X) (A.val.obj X) where
  smul r m := smul Œ± œÜ r m
  one_smul := Sheafify.one_smul Œ± œÜ
  zero_smul := Sheafify.zero_smul Œ± œÜ
  smul_zero := Sheafify.smul_zero Œ± œÜ
  smul_add := Sheafify.smul_add Œ± œÜ
  add_smul := Sheafify.add_smul Œ± œÜ
  mul_smul := Sheafify.mul_smul Œ± œÜ

protected lemma map_smul :
    A.val.map œÄ (smul Œ± œÜ r m) = smul Œ± œÜ (R.val.map œÄ r) (A.val.map œÄ m) := by
  let S := Presheaf.imageSieve Œ± (R.val.map œÄ r) ‚äì Presheaf.imageSieve œÜ (A.val.map œÄ m)
  have hS : S ‚àà J Y.unop := by
    apply J.intersection_covering
    all_goals apply Presheaf.imageSieve_mem
  apply A.isSeparated _ _ hS
  rintro Y f ‚ü®‚ü®r‚ÇÄ,
    (hr‚ÇÄ : (Œ±.app (Opposite.op Y)).hom r‚ÇÄ = (R.val.map f.op).hom ((R.val.map œÄ).hom r))‚ü©,
    ‚ü®m‚ÇÄ, (hm‚ÇÄ : (œÜ.app _) _ = _)‚ü©‚ü©
  rw [‚Üê ConcreteCategory.comp_apply, ‚Üê Functor.map_comp,
    map_smul_eq Œ± œÜ r m (œÄ ‚â´ f.op) r‚ÇÄ (by rw [hr‚ÇÄ, Functor.map_comp, RingCat.comp_apply]) m‚ÇÄ
      (by rw [hm‚ÇÄ, Functor.map_comp, ConcreteCategory.comp_apply]),
    map_smul_eq Œ± œÜ (R.val.map œÄ r) (A.val.map œÄ m) f.op r‚ÇÄ hr‚ÇÄ m‚ÇÄ hm‚ÇÄ]

end Sheafify

/-- Assuming `Œ± : R‚ÇÄ ‚ü∂ R.val` is the sheafification map of a presheaf of rings `R‚ÇÄ`
and `œÜ : M‚ÇÄ.presheaf ‚ü∂ A.val` is the sheafification map of the underlying
sheaf of abelian groups of a presheaf of modules `M‚ÇÄ` over `R‚ÇÄ`, this is
the sheaf of modules over `R` which is obtained by endowing the sections of
`A.val` with a scalar multiplication. -/
noncomputable def sheafify : SheafOfModules.{v} R where
  val := letI := Sheafify.module Œ± œÜ; ofPresheaf A.val (Sheafify.map_smul _ _)
  isSheaf := A.cond

/-- The canonical morphism from a presheaf of modules to its associated sheaf. -/
noncomputable def toSheafify : M‚ÇÄ ‚ü∂ (restrictScalars Œ±).obj (sheafify Œ± œÜ).val :=
  homMk œÜ (fun X r‚ÇÄ m‚ÇÄ ‚Ü¶ by
    simpa using (Sheafify.map_smul_eq Œ± œÜ (Œ±.app _ r‚ÇÄ) (œÜ.app _ m‚ÇÄ) (ùüô _)
      r‚ÇÄ (by simp) m‚ÇÄ (by simp)).symm)

lemma toSheafify_app_apply (X : C·µí·µñ) (x : M‚ÇÄ.obj X) :
    ((toSheafify Œ± œÜ).app X).hom x = œÜ.app X x := rfl

/-- `@[simp]`-normal form of `toSheafify_app_apply`. -/
@[simp]
lemma toSheafify_app_apply' (X : C·µí·µñ) (x : M‚ÇÄ.obj X) :
    DFunLike.coe (F := (_ ‚Üí‚Çó[_] ‚Üë((ModuleCat.restrictScalars (Œ±.app X).hom).obj _)))
    ((toSheafify Œ± œÜ).app X).hom x = œÜ.app X x := rfl

@[simp]
lemma toPresheaf_map_toSheafify : (toPresheaf R‚ÇÄ).map (toSheafify Œ± œÜ) = œÜ := rfl

instance : IsLocallyInjective J (toSheafify Œ± œÜ) := by
  dsimp [IsLocallyInjective]; infer_instance

instance : IsLocallySurjective J (toSheafify Œ± œÜ) := by
  dsimp [IsLocallySurjective]; infer_instance

variable [J.WEqualsLocallyBijective AddCommGrpCat.{v}]

/-- The bijection `((sheafify Œ± œÜ).val ‚ü∂ F) ‚âÉ (M‚ÇÄ ‚ü∂ (restrictScalars Œ±).obj F)` which
is part of the universal property of the sheafification of the presheaf of modules `M‚ÇÄ`,
when `F` is a presheaf of modules which is a sheaf. -/
noncomputable def sheafifyHomEquiv' {F : PresheafOfModules.{v} R.val}
    (hF : Presheaf.IsSheaf J F.presheaf) :
    ((sheafify Œ± œÜ).val ‚ü∂ F) ‚âÉ (M‚ÇÄ ‚ü∂ (restrictScalars Œ±).obj F) :=
  (restrictHomEquivOfIsLocallySurjective Œ± hF).trans
    (homEquivOfIsLocallyBijective (f := toSheafify Œ± œÜ)
      (N := (restrictScalars Œ±).obj F) hF)

lemma comp_toPresheaf_map_sheafifyHomEquiv'_symm_hom {F : PresheafOfModules.{v} R.val}
    (hF : Presheaf.IsSheaf J F.presheaf) (f : M‚ÇÄ ‚ü∂ (restrictScalars Œ±).obj F) :
    œÜ ‚â´ (toPresheaf R.val).map ((sheafifyHomEquiv' Œ± œÜ hF).symm f) = (toPresheaf R‚ÇÄ).map f :=
  (toPresheaf _).congr_map ((sheafifyHomEquiv' Œ± œÜ hF).apply_symm_apply f)

/-- The bijection
`(sheafify Œ± œÜ ‚ü∂ F) ‚âÉ (M‚ÇÄ ‚ü∂ (restrictScalars Œ±).obj ((SheafOfModules.forget _).obj F))`
which is part of the universal property of the sheafification of the presheaf of modules `M‚ÇÄ`,
for any sheaf of modules `F`, see `PresheafOfModules.sheafificationAdjunction` -/
noncomputable def sheafifyHomEquiv {F : SheafOfModules.{v} R} :
    (sheafify Œ± œÜ ‚ü∂ F) ‚âÉ
      (M‚ÇÄ ‚ü∂ (restrictScalars Œ±).obj ((SheafOfModules.forget _).obj F)) :=
  (SheafOfModules.fullyFaithfulForget R).homEquiv.trans
    (sheafifyHomEquiv' Œ± œÜ F.isSheaf)

section

variable {M‚ÇÄ' : PresheafOfModules.{v} R‚ÇÄ} {A' : Sheaf J AddCommGrpCat.{v}}
  (œÜ' : M‚ÇÄ'.presheaf ‚ü∂ A'.val)
  [Presheaf.IsLocallyInjective J œÜ'] [Presheaf.IsLocallySurjective J œÜ']
  (œÑ‚ÇÄ : M‚ÇÄ ‚ü∂ M‚ÇÄ') (œÑ : A ‚ü∂ A')

/-- The morphism of sheaves of modules `sheafify Œ± œÜ ‚ü∂ sheafify Œ± œÜ'`
induced by morphisms `œÑ‚ÇÄ : M‚ÇÄ ‚ü∂ M‚ÇÄ'` and `œÑ : A ‚ü∂ A'`
which satisfy `œÑ‚ÇÄ.hom ‚â´ œÜ' = œÜ ‚â´ œÑ.val`. -/
@[simps]
noncomputable def sheafifyMap (fac : (toPresheaf R‚ÇÄ).map œÑ‚ÇÄ ‚â´ œÜ' = œÜ ‚â´ œÑ.val) :
    sheafify Œ± œÜ ‚ü∂ sheafify Œ± œÜ' where
  val := homMk œÑ.val (fun X r m ‚Ü¶ by
    let f := (sheafifyHomEquiv' Œ± œÜ (by exact A'.cond)).symm (œÑ‚ÇÄ ‚â´ toSheafify Œ± œÜ')
    suffices œÑ.val = (toPresheaf _).map f by simpa only [this] using (f.app X).hom.map_smul r m
    apply ((J.W_of_isLocallyBijective œÜ).homEquiv _ A'.cond).injective
    dsimp [f]
    erw [comp_toPresheaf_map_sheafifyHomEquiv'_symm_hom]
    rw [‚Üê fac, Functor.map_comp, toPresheaf_map_toSheafify])

end

end PresheafOfModules
