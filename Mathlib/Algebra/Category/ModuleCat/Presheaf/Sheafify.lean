/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Category.ModuleCat.Sheaf.ChangeOfRings
import Mathlib.CategoryTheory.Sites.LocallySurjective

/-!
# The associated sheaf of a presheaf of modules

In this file, given a presheaf of modules `Mâ‚€` over a presheaf of rings `Râ‚€`,
we construct the associated sheaf of `Mâ‚€`. More precisely, if `R` is a sheaf of
rings and `Î± : Râ‚€ âŸ¶ R.val` is locally bijective, and `A` is the sheafification
of the underlying presheaf of abelian groups of `Mâ‚€`, i.e. we have a locally bijective
map `Ï† : Mâ‚€.presheaf âŸ¶ A.val`, then we endow `A` with the structure of a
sheaf of modules over `R`: this is `PresheafOfModules.sheafify Î± Ï†`.

In many application, the morphism `Î±` shall be the identity, but this more
general construction allows the sheafification of both the presheaf of rings
and the presheaf of modules.

-/

universe w v vâ‚ uâ‚ u

open CategoryTheory

variable {C : Type uâ‚} [Category.{vâ‚} C] {J : GrothendieckTopology C}

namespace CategoryTheory

namespace Presieve.FamilyOfElements

attribute [local instance] ConcreteCategory.hasCoeToSort ConcreteCategory.instFunLike

section smul

variable {R : Cáµ’áµ– â¥¤ RingCat.{u}} {M : PresheafOfModules.{v} R} {X : C} {P : Presieve X}
  (r : FamilyOfElements (R â‹™ forget _) P) (m : FamilyOfElements (M.presheaf â‹™ forget _) P)

/-- The scalar multiplication of family of elements of a presheaf of modules `M` over `R`
by a family of elements of `R`. -/
def smul : FamilyOfElements (M.presheaf â‹™ forget _) P := fun Y f hf =>
  HSMul.hSMul (Î± := R.obj (Opposite.op Y)) (Î² := M.presheaf.obj (Opposite.op Y)) (r f hf) (m f hf)

end smul

section

variable {Râ‚€ R : Cáµ’áµ– â¥¤ RingCat.{u}} (Î± : Râ‚€ âŸ¶ R) [Presheaf.IsLocallyInjective J Î±]
  {Mâ‚€ : PresheafOfModules.{v} Râ‚€} {A : Cáµ’áµ– â¥¤ AddCommGroupCat.{v}} (Ï† : Mâ‚€.presheaf âŸ¶ A)
  [Presheaf.IsLocallyInjective J Ï†] (hA : Presheaf.IsSeparated J A)
  {X : C} (r : R.obj (Opposite.op X)) (m : A.obj (Opposite.op X)) {P : Presieve X}
  (râ‚€ : FamilyOfElements (Râ‚€ â‹™ forget _) P) (mâ‚€ : FamilyOfElements (Mâ‚€.presheaf â‹™ forget _) P)
  (hrâ‚€ : (râ‚€.map (whiskerRight Î± (forget _))).IsAmalgamation r)
  (hmâ‚€ : (mâ‚€.map (whiskerRight Ï† (forget _))).IsAmalgamation m)

lemma _root_.PresheafOfModules.Sheafify.app_eq_of_isLocallyInjective
    {Y : C} (râ‚€ râ‚€' : Râ‚€.obj (Opposite.op Y))
    (mâ‚€ mâ‚€' : Mâ‚€.presheaf.obj (Opposite.op Y))
    (hrâ‚€ : Î±.app _ râ‚€ = Î±.app _ râ‚€')
    (hmâ‚€ : Ï†.app _ mâ‚€ = Ï†.app _ mâ‚€') :
    Ï†.app _ (râ‚€ â€¢ mâ‚€) = Ï†.app _ (râ‚€' â€¢ mâ‚€') := by
  apply hA _ (Presheaf.equalizerSieve râ‚€ râ‚€' âŠ“ Presheaf.equalizerSieve (F := Mâ‚€.presheaf) mâ‚€ mâ‚€')
  Â· apply J.intersection_covering
    Â· exact Presheaf.equalizerSieve_mem J Î± _ _ hrâ‚€
    Â· exact Presheaf.equalizerSieve_mem J Ï† _ _ hmâ‚€
  Â· intro Z g hg
    erw [â† NatTrans.naturality_apply, â† NatTrans.naturality_apply, Mâ‚€.map_smul, Mâ‚€.map_smul,
      hg.1, hg.2]
    rfl

lemma isCompatible_map_smul_aux {Y Z : C} (f : Y âŸ¶ X) (g : Z âŸ¶ Y)
    (râ‚€ : Râ‚€.obj (Opposite.op Y)) (râ‚€' : Râ‚€.obj (Opposite.op Z))
    (mâ‚€ : Mâ‚€.presheaf.obj (Opposite.op Y)) (mâ‚€' : Mâ‚€.presheaf.obj (Opposite.op Z))
    (hrâ‚€ : Î±.app _ râ‚€ = R.map f.op r) (hrâ‚€' : Î±.app _ râ‚€' = R.map (f.op â‰« g.op) r)
    (hmâ‚€ : Ï†.app _ mâ‚€ = A.map f.op m) (hmâ‚€' : Ï†.app _ mâ‚€' = A.map (f.op â‰« g.op) m) :
    Ï†.app _ (Mâ‚€.presheaf.map g.op (râ‚€ â€¢ mâ‚€)) = Ï†.app _ (râ‚€' â€¢ mâ‚€') := by
  rw [â† PresheafOfModules.Sheafify.app_eq_of_isLocallyInjective Î± Ï† hA (Râ‚€.map g.op râ‚€) râ‚€'
    (Mâ‚€.presheaf.map g.op mâ‚€) mâ‚€', Mâ‚€.map_smul]
  Â· rw [hrâ‚€', R.map_comp, comp_apply, â† hrâ‚€, NatTrans.naturality_apply]
  Â· rw [hmâ‚€', A.map_comp, AddCommGroupCat.coe_comp, Function.comp_apply, â† hmâ‚€]
    erw [NatTrans.naturality_apply]
    rfl

lemma isCompatible_map_smul : ((râ‚€.smul mâ‚€).map (whiskerRight Ï† (forget _))).Compatible := by
  intro Yâ‚ Yâ‚‚ Z gâ‚ gâ‚‚ fâ‚ fâ‚‚ hâ‚ hâ‚‚ fac
  let aâ‚ := râ‚€ fâ‚ hâ‚
  let bâ‚ := mâ‚€ fâ‚ hâ‚
  let aâ‚‚ := râ‚€ fâ‚‚ hâ‚‚
  let bâ‚‚ := mâ‚€ fâ‚‚ hâ‚‚
  let aâ‚€ := Râ‚€.map gâ‚.op aâ‚
  let bâ‚€ := Mâ‚€.map gâ‚.op bâ‚
  have haâ‚ : (Î±.app (Opposite.op Yâ‚)) aâ‚ = (R.map fâ‚.op) r := (hrâ‚€ fâ‚ hâ‚).symm
  have haâ‚‚ : (Î±.app (Opposite.op Yâ‚‚)) aâ‚‚ = (R.map fâ‚‚.op) r := (hrâ‚€ fâ‚‚ hâ‚‚).symm
  have hbâ‚ : (Ï†.app (Opposite.op Yâ‚)) bâ‚ = (A.map fâ‚.op) m := (hmâ‚€ fâ‚ hâ‚).symm
  have hbâ‚‚ : (Ï†.app (Opposite.op Yâ‚‚)) bâ‚‚ = (A.map fâ‚‚.op) m := (hmâ‚€ fâ‚‚ hâ‚‚).symm
  have haâ‚€ : (Î±.app (Opposite.op Z)) aâ‚€ = (R.map (fâ‚.op â‰« gâ‚.op)) r := by
    dsimp [aâ‚€]
    rw [NatTrans.naturality_apply, haâ‚, Functor.map_comp, comp_apply]
  have hbâ‚€ : (Ï†.app (Opposite.op Z)) bâ‚€ = (A.map (fâ‚.op â‰« gâ‚.op)) m := by
    dsimp [bâ‚€]
    erw [NatTrans.naturality_apply, hbâ‚, Functor.map_comp, comp_apply]
    rfl
  have haâ‚€' : (Î±.app (Opposite.op Z)) aâ‚€ = (R.map (fâ‚‚.op â‰« gâ‚‚.op)) r := by
    rw [haâ‚€, â† op_comp, fac, op_comp]
  have hbâ‚€' : (Ï†.app (Opposite.op Z)) bâ‚€ = (A.map (fâ‚‚.op â‰« gâ‚‚.op)) m := by
    rw [hbâ‚€, â† op_comp, fac, op_comp]
  dsimp
  erw [â† NatTrans.naturality_apply, â† NatTrans.naturality_apply]
  exact (isCompatible_map_smul_aux Î± Ï† hA r m fâ‚ gâ‚ aâ‚ aâ‚€ bâ‚ bâ‚€ haâ‚ haâ‚€ hbâ‚ hbâ‚€).trans
    (isCompatible_map_smul_aux Î± Ï† hA r m fâ‚‚ gâ‚‚ aâ‚‚ aâ‚€ bâ‚‚ bâ‚€ haâ‚‚ haâ‚€' hbâ‚‚ hbâ‚€').symm

end

end Presieve.FamilyOfElements

end CategoryTheory

variable {Râ‚€ : Cáµ’áµ– â¥¤ RingCat.{u}} {R : Sheaf J RingCat.{u}} (Î± : Râ‚€ âŸ¶ R.val)
  [Presheaf.IsLocallyInjective J Î±] [Presheaf.IsLocallySurjective J Î±]

namespace PresheafOfModules

variable {Mâ‚€ : PresheafOfModules.{v} Râ‚€} {A : Sheaf J AddCommGroupCat.{v}}
  (Ï† : Mâ‚€.presheaf âŸ¶ A.val)
  [Presheaf.IsLocallyInjective J Ï†] [Presheaf.IsLocallySurjective J Ï†]

namespace Sheafify

variable {X Y : Cáµ’áµ–} (Ï€ : X âŸ¶ Y) (r r' : R.val.obj X) (m m' : A.val.obj X)

/-- Assuming `Î± : Râ‚€ âŸ¶ R.val` is the sheafification map of a presheaf of rings `Râ‚€`
and `Ï† : Mâ‚€.presheaf âŸ¶ A.val` is the sheafification map of the underlying
sheaf of abelian groups of a presheaf of modules `Mâ‚€` over `Râ‚€`, then given
`r : R.val.obj X` and `m : A.val.obj X`, this structure contains the data
of `x : A.val.obj X` along with the property which makes `x` a good candidate
for the definition of the scalar multiplication `r â€¢ m`. -/
structure SMulCandidate where
  /-- The candidate for the scalar product `r â€¢ m`. -/
  x : A.val.obj X
  h â¦ƒY : Cáµ’áµ–â¦„ (f : X âŸ¶ Y) (râ‚€ : Râ‚€.obj Y) (hrâ‚€ : Î±.app Y râ‚€ = R.val.map f r)
    (mâ‚€ : Mâ‚€.obj Y) (hmâ‚€ : Ï†.app Y mâ‚€ = A.val.map f m) : A.val.map f x = Ï†.app Y (râ‚€ â€¢ mâ‚€)

/-- Constructor for `SMulCandidate`. -/
def SMulCandidate.mk' (S : Sieve X.unop) (hS : S âˆˆ J X.unop)
    (râ‚€ : Presieve.FamilyOfElements (Râ‚€ â‹™ forget _) S.arrows)
    (mâ‚€ : Presieve.FamilyOfElements (Mâ‚€.presheaf â‹™ forget _) S.arrows)
    (hrâ‚€ : (râ‚€.map (whiskerRight Î± (forget _))).IsAmalgamation r)
    (hmâ‚€ : (mâ‚€.map (whiskerRight Ï† (forget _))).IsAmalgamation m)
    (a : A.val.obj X)
    (ha : ((râ‚€.smul mâ‚€).map (whiskerRight Ï† (forget _))).IsAmalgamation a) :
    SMulCandidate Î± Ï† r m where
  x := a
  h Y f aâ‚€ haâ‚€ bâ‚€ hbâ‚€ := by
    apply A.isSeparated _ _ (J.pullback_stable f.unop hS)
    rintro Z g hg
    dsimp at hg
    erw [â† comp_apply, â† A.val.map_comp, â† NatTrans.naturality_apply, Mâ‚€.map_smul]
    refine (ha _ hg).trans (app_eq_of_isLocallyInjective Î± Ï† A.isSeparated _ _ _ _ ?_ ?_)
    Â· rw [NatTrans.naturality_apply, haâ‚€]
      apply (hrâ‚€ _ hg).symm.trans
      dsimp
      rw [Functor.map_comp, comp_apply]
    Â· erw [NatTrans.naturality_apply, hbâ‚€]
      apply (hmâ‚€ _ hg).symm.trans
      dsimp
      rw [Functor.map_comp]
      rfl

instance : Nonempty (SMulCandidate Î± Ï† r m) := âŸ¨by
  let S := (Presheaf.imageSieve Î± r âŠ“ Presheaf.imageSieve Ï† m)
  have hS : S âˆˆ J _ := by
    apply J.intersection_covering
    all_goals apply Presheaf.imageSieve_mem
  have hâ‚ : S â‰¤ Presheaf.imageSieve Î± r := fun _ _ h => h.1
  have hâ‚‚ : S â‰¤ Presheaf.imageSieve Ï† m := fun _ _ h => h.2
  let râ‚€ := (Presieve.FamilyOfElements.localPreimage (whiskerRight Î± (forget _)) r).restrict hâ‚
  let mâ‚€ := (Presieve.FamilyOfElements.localPreimage (whiskerRight Ï† (forget _)) m).restrict hâ‚‚
  have hrâ‚€ : (râ‚€.map (whiskerRight Î± (forget _))).IsAmalgamation r := by
    rw [Presieve.FamilyOfElements.restrict_map]
    apply Presieve.isAmalgamation_restrict
    apply Presieve.FamilyOfElements.isAmalgamation_map_localPreimage
  have hmâ‚€ : (mâ‚€.map (whiskerRight Ï† (forget _))).IsAmalgamation m := by
    rw [Presieve.FamilyOfElements.restrict_map]
    apply Presieve.isAmalgamation_restrict
    apply Presieve.FamilyOfElements.isAmalgamation_map_localPreimage
  exact SMulCandidate.mk' Î± Ï† r m S hS râ‚€ mâ‚€ hrâ‚€ hmâ‚€ _ (Presieve.IsSheafFor.isAmalgamation
    (((sheafCompose J (forget _)).obj A).2.isSheafFor S hS)
    (Presieve.FamilyOfElements.isCompatible_map_smul Î± Ï† A.isSeparated r m râ‚€ mâ‚€ hrâ‚€ hmâ‚€))âŸ©

instance : Subsingleton (SMulCandidate Î± Ï† r m) where
  allEq := by
    rintro âŸ¨xâ‚, hâ‚âŸ© âŸ¨xâ‚‚, hâ‚‚âŸ©
    simp only [SMulCandidate.mk.injEq]
    let S := (Presheaf.imageSieve Î± r âŠ“ Presheaf.imageSieve Ï† m)
    have hS : S âˆˆ J _ := by
      apply J.intersection_covering
      all_goals apply Presheaf.imageSieve_mem
    apply A.isSeparated _ _ hS
    intro Y f âŸ¨âŸ¨râ‚€, hrâ‚€âŸ©, âŸ¨mâ‚€, hmâ‚€âŸ©âŸ©
    erw [hâ‚ f.op râ‚€ hrâ‚€ mâ‚€ hmâ‚€, hâ‚‚ f.op râ‚€ hrâ‚€ mâ‚€ hmâ‚€]

noncomputable instance : Unique (SMulCandidate Î± Ï† r m) :=
  uniqueOfSubsingleton (Nonempty.some inferInstance)

/-- The (unique) element in `SMulCandidate Î± Ï† r m`. -/
noncomputable def smulCandidate : SMulCandidate Î± Ï† r m := default

/-- The scalar multiplication on the sheafification of a presheaf of modules. -/
noncomputable def smul : A.val.obj X := (smulCandidate Î± Ï† r m).x

lemma map_smul_eq {Y : Cáµ’áµ–} (f : X âŸ¶ Y) (râ‚€ : Râ‚€.obj Y) (hrâ‚€ : Î±.app Y râ‚€ = R.val.map f r)
    (mâ‚€ : Mâ‚€.obj Y) (hmâ‚€ : Ï†.app Y mâ‚€ = A.val.map f m) :
    A.val.map f (smul Î± Ï† r m) = Ï†.app Y (râ‚€ â€¢ mâ‚€) :=
  (smulCandidate Î± Ï† r m).h f râ‚€ hrâ‚€ mâ‚€ hmâ‚€

protected lemma one_smul : smul Î± Ï† 1 m = m := by
  apply A.isSeparated _ _ (Presheaf.imageSieve_mem J Ï† m)
  rintro Y f âŸ¨mâ‚€, hmâ‚€âŸ©
  rw [â† hmâ‚€]
  erw [map_smul_eq Î± Ï† 1 m f.op 1 (by simp) mâ‚€ hmâ‚€, one_smul]
  rfl

protected lemma zero_smul : smul Î± Ï† 0 m = 0 := by
  apply A.isSeparated _ _ (Presheaf.imageSieve_mem J Ï† m)
  rintro Y f âŸ¨mâ‚€, hmâ‚€âŸ©
  erw [map_smul_eq Î± Ï† 0 m f.op 0 (by simp) mâ‚€ hmâ‚€, zero_smul, map_zero,
    (A.val.map f.op).map_zero]

protected lemma smul_zero : smul Î± Ï† r 0 = 0 := by
  apply A.isSeparated _ _ (Presheaf.imageSieve_mem J Î± r)
  rintro Y f âŸ¨râ‚€, hrâ‚€âŸ©
  erw [(A.val.map f.op).map_zero, map_smul_eq Î± Ï† r 0 f.op râ‚€ hrâ‚€ 0 (by simp),
    smul_zero, map_zero]

protected lemma smul_add : smul Î± Ï† r (m + m') = smul Î± Ï† r m + smul Î± Ï† r m' := by
  let S := Presheaf.imageSieve Î± r âŠ“ Presheaf.imageSieve Ï† m âŠ“ Presheaf.imageSieve Ï† m'
  have hS : S âˆˆ J X.unop := by
    refine J.intersection_covering (J.intersection_covering ?_ ?_) ?_
    all_goals apply Presheaf.imageSieve_mem
  apply A.isSeparated _ _ hS
  rintro Y f âŸ¨âŸ¨âŸ¨râ‚€, hrâ‚€âŸ©, âŸ¨mâ‚€ : Mâ‚€.presheaf.obj _, hmâ‚€âŸ©âŸ©, âŸ¨mâ‚€' : Mâ‚€.presheaf.obj _, hmâ‚€'âŸ©âŸ©
  erw [(A.val.map f.op).map_add, map_smul_eq Î± Ï† r m f.op râ‚€ hrâ‚€ mâ‚€ hmâ‚€,
    map_smul_eq Î± Ï† r m' f.op râ‚€ hrâ‚€ mâ‚€' hmâ‚€',
    map_smul_eq Î± Ï† r (m + m') f.op râ‚€ hrâ‚€ (mâ‚€ + mâ‚€')
      (by erw [map_add, map_add, hmâ‚€, hmâ‚€']; rfl),
    smul_add, map_add]

protected lemma add_smul : smul Î± Ï† (r + r') m = smul Î± Ï† r m + smul Î± Ï† r' m := by
  let S := Presheaf.imageSieve Î± r âŠ“ Presheaf.imageSieve Î± r' âŠ“ Presheaf.imageSieve Ï† m
  have hS : S âˆˆ J X.unop := by
    refine J.intersection_covering (J.intersection_covering ?_ ?_) ?_
    all_goals apply Presheaf.imageSieve_mem
  apply A.isSeparated _ _ hS
  rintro Y f âŸ¨âŸ¨âŸ¨râ‚€ : Râ‚€.obj _, hrâ‚€âŸ©, âŸ¨râ‚€' : Râ‚€.obj _, hrâ‚€'âŸ©âŸ©, âŸ¨mâ‚€, hmâ‚€âŸ©âŸ©
  erw [(A.val.map f.op).map_add, map_smul_eq Î± Ï† r m f.op râ‚€ hrâ‚€ mâ‚€ hmâ‚€,
    map_smul_eq Î± Ï† r' m f.op râ‚€' hrâ‚€' mâ‚€ hmâ‚€,
    map_smul_eq Î± Ï† (r + r') m f.op (râ‚€ + râ‚€') (by rw [map_add, map_add, hrâ‚€, hrâ‚€'])
      mâ‚€ hmâ‚€, add_smul, map_add]

protected lemma mul_smul : smul Î± Ï† (r * r') m = smul Î± Ï† r (smul Î± Ï† r' m) := by
  let S := Presheaf.imageSieve Î± r âŠ“ Presheaf.imageSieve Î± r' âŠ“ Presheaf.imageSieve Ï† m
  have hS : S âˆˆ J X.unop := by
    refine J.intersection_covering (J.intersection_covering ?_ ?_) ?_
    all_goals apply Presheaf.imageSieve_mem
  apply A.isSeparated _ _ hS
  rintro Y f âŸ¨âŸ¨âŸ¨râ‚€ : Râ‚€.obj _, hrâ‚€âŸ©, âŸ¨râ‚€' : Râ‚€.obj _, hrâ‚€'âŸ©âŸ©, âŸ¨mâ‚€ : Mâ‚€.presheaf.obj _, hmâ‚€âŸ©âŸ©
  erw [map_smul_eq Î± Ï† (r * r') m f.op (râ‚€ * râ‚€')
    (by rw [map_mul, map_mul, hrâ‚€, hrâ‚€']) mâ‚€ hmâ‚€, mul_smul,
    map_smul_eq Î± Ï† r (smul Î± Ï† r' m) f.op râ‚€ hrâ‚€ (râ‚€' â€¢ mâ‚€)
      (map_smul_eq Î± Ï† r' m f.op râ‚€' hrâ‚€' mâ‚€ hmâ‚€).symm]

variable (X)

/-- The module structure on the sections of the sheafification of the underlying
presheaf of abelian groups of a presheaf of modules. -/
noncomputable def module : Module (R.val.obj X) (A.val.obj X) where
  smul r m := smul Î± Ï† r m
  one_smul := Sheafify.one_smul Î± Ï†
  zero_smul := Sheafify.zero_smul Î± Ï†
  smul_zero := Sheafify.smul_zero Î± Ï†
  smul_add := Sheafify.smul_add Î± Ï†
  add_smul := Sheafify.add_smul Î± Ï†
  mul_smul := Sheafify.mul_smul Î± Ï†

lemma map_smul :
    A.val.map Ï€ (smul Î± Ï† r m) = smul Î± Ï† (R.val.map Ï€ r) (A.val.map Ï€ m) := by
  let S := Presheaf.imageSieve Î± (R.val.map Ï€ r) âŠ“ Presheaf.imageSieve Ï† (A.val.map Ï€ m)
  have hS : S âˆˆ J Y.unop := by
    apply J.intersection_covering
    all_goals apply Presheaf.imageSieve_mem
  apply A.isSeparated _ _ hS
  rintro Y f âŸ¨âŸ¨râ‚€, hrâ‚€âŸ©, âŸ¨mâ‚€, hmâ‚€âŸ©âŸ©
  erw [â† comp_apply, â† Functor.map_comp,
    map_smul_eq Î± Ï† r m (Ï€ â‰« f.op) râ‚€ (by rw [hrâ‚€, Functor.map_comp, comp_apply]) mâ‚€
      (by erw [hmâ‚€, Functor.map_comp, comp_apply]; rfl),
    map_smul_eq Î± Ï† (R.val.map Ï€ r) (A.val.map Ï€ m) f.op râ‚€ hrâ‚€ mâ‚€ hmâ‚€]

end Sheafify

/-- Assuming `Î± : Râ‚€ âŸ¶ R.val` is the sheafification map of a presheaf of rings `Râ‚€`
and `Ï† : Mâ‚€.presheaf âŸ¶ A.val` is the sheafification map of the underlying
sheaf of abelian groups of a presheaf of modules `Mâ‚€` over `Râ‚€`, this is
the sheaf of modules over `R` which is obtained by endowing the sections of
`A.val` with a scalar multiplication. -/
noncomputable def sheafify : SheafOfModules.{v} R where
  val :=
    { presheaf := A.val
      module := Sheafify.module Î± Ï†
      map_smul := fun _ _ _ => by apply Sheafify.map_smul }
  isSheaf := A.cond

/-- The canonical morphism from a presheaf of modules to its associated sheaf. -/
@[simps]
def toSheafify : Mâ‚€ âŸ¶ (restrictScalars Î±).obj (sheafify Î± Ï†).val where
  hom := Ï†
  map_smul X râ‚€ mâ‚€ := by
    simpa using (Sheafify.map_smul_eq Î± Ï† (Î±.app _ râ‚€) (Ï†.app _ mâ‚€) (ğŸ™ _)
      râ‚€ (by aesop) mâ‚€ (by simp)).symm

instance : Presheaf.IsLocallyInjective J (toSheafify Î± Ï†).hom := by
  dsimp; infer_instance

instance : Presheaf.IsLocallySurjective J (toSheafify Î± Ï†).hom := by
  dsimp; infer_instance

variable [J.WEqualsLocallyBijective AddCommGroupCat.{v}]

/-- The bijection `((sheafify Î± Ï†).val âŸ¶ F) â‰ƒ (Mâ‚€ âŸ¶ (restrictScalars Î±).obj F)` which
is part of the universal property of the sheafification of the presheaf of modules `Mâ‚€`,
when `F` is a presheaf of modules which is a sheaf. -/
noncomputable def sheafifyHomEquiv' {F : PresheafOfModules.{v} R.val}
    (hF : Presheaf.IsSheaf J F.presheaf) :
    ((sheafify Î± Ï†).val âŸ¶ F) â‰ƒ (Mâ‚€ âŸ¶ (restrictScalars Î±).obj F) :=
  (restrictHomEquivOfIsLocallySurjective Î± hF).trans
    (homEquivOfIsLocallyBijective (f := toSheafify Î± Ï†)
      (N := (restrictScalars Î±).obj F) hF)

lemma comp_sheafifyHomEquiv'_symm_hom {F : PresheafOfModules.{v} R.val}
    (hF : Presheaf.IsSheaf J F.presheaf) (f : Mâ‚€ âŸ¶ (restrictScalars Î±).obj F) :
    Ï† â‰« ((sheafifyHomEquiv' Î± Ï† hF).symm f).hom = f.hom :=
  congr_arg Hom.hom ((sheafifyHomEquiv' Î± Ï† hF).apply_symm_apply f)

/-- The bijection
`(sheafify Î± Ï† âŸ¶ F) â‰ƒ (Mâ‚€ âŸ¶ (restrictScalars Î±).obj ((SheafOfModules.forget _).obj F))`
which is part of the universal property of the sheafification of the presheaf of modules `Mâ‚€`,
for any sheaf of modules `F`, see `PresheafOfModules.sheafificationAdjunction` -/
noncomputable def sheafifyHomEquiv {F : SheafOfModules.{v} R} :
    (sheafify Î± Ï† âŸ¶ F) â‰ƒ
      (Mâ‚€ âŸ¶ (restrictScalars Î±).obj ((SheafOfModules.forget _).obj F)) :=
  (SheafOfModules.fullyFaithfulForget R).homEquiv.trans
    (sheafifyHomEquiv' Î± Ï† F.isSheaf)

section

variable {Mâ‚€' : PresheafOfModules.{v} Râ‚€} {A' : Sheaf J AddCommGroupCat.{v}}
  (Ï†' : Mâ‚€'.presheaf âŸ¶ A'.val)
  [Presheaf.IsLocallyInjective J Ï†'] [Presheaf.IsLocallySurjective J Ï†']
  (Ï„â‚€ : Mâ‚€ âŸ¶ Mâ‚€') (Ï„ : A âŸ¶ A')
  (fac : Ï„â‚€.hom â‰« Ï†' = Ï† â‰« Ï„.val)

/-- The morphism of sheaves of modules `sheafify Î± Ï† âŸ¶ sheafify Î± Ï†'`
induced by morphisms `Ï„â‚€ : Mâ‚€ âŸ¶ Mâ‚€'` and `Ï„ : A âŸ¶ A'`
which satisfy `Ï„â‚€.hom â‰« Ï†' = Ï† â‰« Ï„.val`. -/
@[simps]
def sheafifyMap : sheafify Î± Ï† âŸ¶ sheafify Î± Ï†' where
  val :=
    { hom := Ï„.val
      map_smul := by
        let f := (sheafifyHomEquiv' Î± Ï† (by exact A'.cond)).symm (Ï„â‚€ â‰« toSheafify Î± Ï†')
        have eq : Ï„.val = f.hom := ((J.W_of_isLocallyBijective Ï†).homEquiv _ A'.cond).injective
          (by
            dsimp [f]
            erw [comp_sheafifyHomEquiv'_symm_hom]
            simp only [â† fac, toSheafify_hom, Hom.comp_hom])
        convert f.map_smul }

end

end PresheafOfModules
