/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Category.ModuleCat.ChangeOfRings
import Mathlib.Algebra.Category.ModuleCat.Presheaf

/-!
# Change of presheaf of rings

In this file, we define the restriction of scalars functor
`restrictScalars Œ± : PresheafOfModules.{v} R' ‚•§ PresheafOfModules.{v} R`
attached to a morphism of presheaves of rings `Œ± : R ‚ü∂ R'`.

-/

universe v v' u u'

open CategoryTheory

namespace PresheafOfModules

variable {C : Type u'} [Category.{v'} C] {R R' : C·µí·µñ ‚•§ RingCat.{u}}

/-- The restriction of scalars of presheaves of modules, on objects. -/
@[simps]
noncomputable def restrictScalarsObj (M' : PresheafOfModules.{v} R') (Œ± : R ‚ü∂ R') :
    PresheafOfModules R where
  obj := fun X ‚Ü¶ (ModuleCat.restrictScalars (Œ±.app X)).obj (M'.obj X)
  map := fun {X Y} f ‚Ü¶
    { toFun := M'.map f
      map_add' := map_add _
      map_smul' := fun r x ‚Ü¶ (M'.map_smul f (Œ±.app _ r) x).trans (by
        have eq := RingHom.congr_fun (Œ±.naturality f) r
        dsimp at eq
        rw [‚Üê eq]
        rfl ) }

/-- The restriction of scalars functor `PresheafOfModules R' ‚•§ PresheafOfModules R`
induced by a morphism of presheaves of rings `R ‚ü∂ R'`. -/
@[simps]
noncomputable def restrictScalars (Œ± : R ‚ü∂ R') :
    PresheafOfModules.{v} R' ‚•§ PresheafOfModules.{v} R where
  obj M' := M'.restrictScalarsObj Œ±
  map œÜ' :=
    { app := fun X ‚Ü¶ (ModuleCat.restrictScalars (Œ±.app X)).map (Hom.app œÜ' X)
      naturality := fun {X Y} f ‚Ü¶ by
        ext x
        exact naturality_apply œÜ' f x }

instance (Œ± : R ‚ü∂ R') : (restrictScalars.{v} Œ±).Additive where

instance : (restrictScalars (ùüô R)).Full := inferInstanceAs (ùü≠ _).Full

instance (Œ± : R ‚ü∂ R') : (restrictScalars Œ±).Faithful where
  map_injective h := (toPresheaf R').map_injective ((toPresheaf R).congr_map h)

/-- The isomorphism `restrictScalars Œ± ‚ãô toPresheaf R ‚âÖ toPresheaf R'` for any
morphism of presheaves of rings `Œ± : R ‚ü∂ R'`. -/
noncomputable def restrictScalarsCompToPresheaf (Œ± : R ‚ü∂ R') :
    restrictScalars.{v} Œ± ‚ãô toPresheaf R ‚âÖ toPresheaf R' := Iso.refl _

end PresheafOfModules
