/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.Algebra.Category.ModuleCat.Presheaf.ChangeOfRings

/-!
# Pushforward of presheaves of modules

If `F : C â¥¤ D`, the precomposition `F.op â‹™ _` induces a functor from presheaves
over `D` to presheaves over `C`. When `R : Dáµ’áµ– â¥¤ RingCat`, we define the
induced functor `pushforwardâ‚€ : PresheafOfModules.{v} R â¥¤ PresheafOfModules.{v} (F.op â‹™ R)`
on presheaves of modules.

In case we have a morphism of presheaves of rings `S âŸ¶ F.op â‹™ R`, we also construct
a functor `pushforward : PresheafOfModules.{v} R â¥¤ PresheafOfModules.{v} S`.

-/

universe v vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ uâ‚ uâ‚‚ uâ‚ƒ uâ‚„ u

open CategoryTheory

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D]
  {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E] {E' : Type uâ‚„} [Category.{vâ‚„} E']

namespace PresheafOfModules

variable (F : C â¥¤ D)

/-- The pushforward functor on presheaves of modules for a functor `F : C â¥¤ D` and
`R : Dáµ’áµ– â¥¤ RingCat`. On the underlying presheaves of abelian groups, it is induced
by the precomposition with `F.op`. -/
def pushforwardâ‚€ (R : Dáµ’áµ– â¥¤ RingCat.{u}) :
    PresheafOfModules.{v} R â¥¤ PresheafOfModules.{v} (F.op â‹™ R) where
  obj M :=
    { obj := fun X â†¦ ModuleCat.of _ (M.obj (F.op.obj X))
      map := fun {X Y} f â†¦ M.map (F.op.map f)
      map_id := fun X â†¦ by
        refine ModuleCat.hom_ext
          -- Work around an instance diamond for `restrictScalarsId'`
          (@LinearMap.ext _ _ _ _ _ _ _ _ (_) (_) _ _ _ (fun x => ?_))
        exact (M.congr_map_apply (F.op.map_id X) x).trans (by simp)
      map_comp := fun f g â†¦ by
        refine ModuleCat.hom_ext
          -- Work around an instance diamond for `restrictScalarsId'`
          (@LinearMap.ext _ _ _ _ _ _ _ _ (_) (_) _ _ _ (fun x => ?_))
        exact (M.congr_map_apply (F.op.map_comp f g) x).trans (by simp) }
  map {Mâ‚ Mâ‚‚} Ï† := { app := fun X â†¦ Ï†.app _ }

/-- The pushforward of presheaves of modules commutes with the forgetful functor
to presheaves of abelian groups. -/
def pushforwardâ‚€CompToPresheaf (R : Dáµ’áµ– â¥¤ RingCat.{u}) :
    pushforwardâ‚€.{v} F R â‹™ toPresheaf _ â‰… toPresheaf _ â‹™ (whiskeringLeft _ _ _).obj F.op :=
  Iso.refl _

variable {F}
variable {R : Dáµ’áµ– â¥¤ RingCat.{u}} {S : Cáµ’áµ– â¥¤ RingCat.{u}} (Ï† : S âŸ¶ F.op â‹™ R)

attribute [local simp] pushforwardâ‚€ in
/-- The pushforward functor `PresheafOfModules R â¥¤ PresheafOfModules S` induced by
a morphism of presheaves of rings `S âŸ¶ F.op â‹™ R`. -/
@[simps! obj_obj]
noncomputable def pushforward : PresheafOfModules.{v} R â¥¤ PresheafOfModules.{v} S :=
  pushforwardâ‚€ F R â‹™ restrictScalars Ï†

/-- The pushforward of presheaves of modules commutes with the forgetful functor
to presheaves of abelian groups. -/
noncomputable def pushforwardCompToPresheaf :
    pushforward.{v} Ï† â‹™ toPresheaf _ â‰… toPresheaf _ â‹™ (whiskeringLeft _ _ _).obj F.op :=
  Iso.refl _

lemma pushforward_obj_map_apply (M : PresheafOfModules.{v} R) {X Y : Cáµ’áµ–} (f : X âŸ¶ Y)
    (m : (ModuleCat.restrictScalars (Ï†.app X).hom).obj (M.obj (Opposite.op (F.obj X.unop)))) :
      (((pushforward Ï†).obj M).map f).hom m = M.map (F.map f.unop).op m := rfl

/-- `@[simp]`-normal form of `pushforward_obj_map_apply`. -/
@[simp]
lemma pushforward_obj_map_apply' (M : PresheafOfModules.{v} R) {X Y : Cáµ’áµ–} (f : X âŸ¶ Y)
    (m : (ModuleCat.restrictScalars (Ï†.app X).hom).obj (M.obj (Opposite.op (F.obj X.unop)))) :
      DFunLike.coe
        (F := â†‘((ModuleCat.restrictScalars _).obj _) â†’â‚—[_]
          â†‘((ModuleCat.restrictScalars (S.map f).hom).obj ((ModuleCat.restrictScalars _).obj _)))
        (((pushforward Ï†).obj M).map f).hom m = M.map (F.map f.unop).op m := rfl

lemma pushforward_map_app_apply {M N : PresheafOfModules.{v} R} (Î± : M âŸ¶ N) (X : Cáµ’áµ–)
    (m : (ModuleCat.restrictScalars (Ï†.app X).hom).obj (M.obj (Opposite.op (F.obj X.unop)))) :
    (((pushforward Ï†).map Î±).app X).hom m = Î±.app (Opposite.op (F.obj X.unop)) m := rfl

/-- `@[simp]`-normal form of `pushforward_map_app_apply`. -/
@[simp]
lemma pushforward_map_app_apply' {M N : PresheafOfModules.{v} R} (Î± : M âŸ¶ N) (X : Cáµ’áµ–)
    (m : (ModuleCat.restrictScalars (Ï†.app X).hom).obj (M.obj (Opposite.op (F.obj X.unop)))) :
    DFunLike.coe
      (F := â†‘((ModuleCat.restrictScalars _).obj _) â†’â‚—[_] â†‘((ModuleCat.restrictScalars _).obj _))
      (((pushforward Ï†).map Î±).app X).hom m = Î±.app (Opposite.op (F.obj X.unop)) m := rfl

section

variable (R) in
noncomputable def pushforwardId :
    pushforward.{v} (S := R) (F := ğŸ­ _) (ğŸ™ R) â‰… ğŸ­ _ :=
  Iso.refl _

section

variable {T : Eáµ’áµ– â¥¤ RingCat.{u}} {G : D â¥¤ E} (Ïˆ : R âŸ¶ G.op â‹™ T)

noncomputable def pushforwardComp :
  pushforward.{v} (F := F â‹™ G) (Ï† â‰« whiskerLeft F.op Ïˆ) â‰…
    pushforward.{v} Ïˆ â‹™ pushforward.{v} Ï† :=
  Iso.refl _

variable {T' : E'áµ’áµ– â¥¤ RingCat.{u}} {G' : E â¥¤ E'} (Ïˆ' : T âŸ¶ G'.op â‹™ T')

lemma pushforward_assoc :
    pushforwardComp.{v} (F := F â‹™ G) (Ï† â‰« whiskerLeft F.op Ïˆ) Ïˆ' â‰ªâ‰«
      isoWhiskerLeft _ (pushforwardComp.{v} Ï† Ïˆ) =
    pushforwardComp.{v} (G := G â‹™ G') Ï† (Ïˆ â‰« whiskerLeft G.op Ïˆ') â‰ªâ‰«
      isoWhiskerRight (pushforwardComp.{v} Ïˆ Ïˆ') _ â‰ªâ‰«
        Functor.associator _ _ _ := by ext; rfl

lemma pushforward_hom_app_assoc (M : PresheafOfModules.{v} T') :
    (pushforwardComp (F := F â‹™ G) (Ï† â‰« whiskerLeft F.op Ïˆ) Ïˆ').hom.app M â‰«
      (pushforwardComp Ï† Ïˆ).hom.app _ =
      (pushforwardComp (G := G â‹™ G') Ï† (Ïˆ â‰« whiskerLeft G.op Ïˆ')).hom.app M â‰«
      (pushforward Ï†).map ((pushforwardComp Ïˆ Ïˆ').hom.app _) := by
  rfl

lemma pushforward_inv_app_assoc (M : PresheafOfModules.{v} T') :
    (pushforwardComp Ï† Ïˆ).inv.app _ â‰«
      (pushforwardComp (F := F â‹™ G) (Ï† â‰« whiskerLeft F.op Ïˆ) Ïˆ').inv.app M =
    (pushforward Ï†).map ((pushforwardComp Ïˆ Ïˆ').inv.app _) â‰«
      (pushforwardComp (G := G â‹™ G') Ï† (Ïˆ â‰« whiskerLeft G.op Ïˆ')).inv.app M := rfl

end

lemma pushforward_id_comp :
    pushforwardComp.{v} (F := ğŸ­ C) (ğŸ™ S) Ï† = (Functor.rightUnitor _).symm â‰ªâ‰«
      isoWhiskerLeft (pushforward.{v} Ï†) (pushforwardId S).symm := by ext; rfl

lemma pushforward_comp_id :
    pushforwardComp.{v} (G := ğŸ­ _) Ï† (ğŸ™ R) = (Functor.leftUnitor _).symm â‰ªâ‰«
      isoWhiskerRight (pushforwardId R).symm (pushforward.{v} Ï†) := by ext; rfl

end

end PresheafOfModules
