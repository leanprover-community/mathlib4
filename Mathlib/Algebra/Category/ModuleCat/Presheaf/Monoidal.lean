/-
Copyright (c) 2024 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson, Jack McKoen, JoÃ«l Riou
-/
import Mathlib.Algebra.Category.ModuleCat.Presheaf
import Mathlib.Algebra.Category.ModuleCat.Monoidal.Basic

/-!
# The monoidal category structure on presheaves of modules

Given a presheaf of commutative rings `R : Cáµ’áµ– â¥¤ CommRingCat`, we construct
the monoidal category structure on the category of presheaves of modules
`PresheafOfModules (R â‹™ forgetâ‚‚ _ _)`. The tensor product `Mâ‚ âŠ— Mâ‚‚` is defined
as the presheaf of modules which sends `X : Cáµ’áµ–` to `Mâ‚.obj X âŠ— Mâ‚‚.obj X`.

## Notes

This contribution was created as part of the AIM workshop
"Formalizing algebraic geometry" in June 2024.

-/

open CategoryTheory MonoidalCategory Category

universe v u vâ‚ uâ‚

variable {C : Type*} [Category C] {R : Cáµ’áµ– â¥¤ CommRingCat.{u}}

instance (X : Cáµ’áµ–) : CommRing ((R â‹™ forgetâ‚‚ _ RingCat).obj X) :=
  inferInstanceAs (CommRing (R.obj X))

namespace PresheafOfModules

namespace Monoidal

variable (Mâ‚ Mâ‚‚ Mâ‚ƒ Mâ‚„ : PresheafOfModules.{u} (R â‹™ forgetâ‚‚ _ _))

/-- Auxiliary definition for `tensorObj`. -/
noncomputable def tensorObjMap {X Y : Cáµ’áµ–} (f : X âŸ¶ Y) : Mâ‚.obj X âŠ— Mâ‚‚.obj X âŸ¶
    (ModuleCat.restrictScalars (R.map f).hom).obj (Mâ‚.obj Y âŠ— Mâ‚‚.obj Y) :=
  ModuleCat.MonoidalCategory.tensorLift (fun mâ‚ mâ‚‚ â†¦ Mâ‚.map f mâ‚ âŠ—â‚œ Mâ‚‚.map f mâ‚‚)
    (by intro mâ‚ mâ‚' mâ‚‚; dsimp; rw [map_add, TensorProduct.add_tmul])
    (by intro a mâ‚ mâ‚‚; dsimp; erw [Mâ‚.map_smul]; rfl)
    (by intro mâ‚ mâ‚‚ mâ‚‚'; dsimp; rw [map_add, TensorProduct.tmul_add])
    (by intro a mâ‚ mâ‚‚; dsimp; erw [Mâ‚‚.map_smul, TensorProduct.tmul_smul (r := R.map f a)]; rfl)

/-- The tensor product of two presheaves of modules. -/
@[simps obj]
noncomputable def tensorObj : PresheafOfModules (R â‹™ forgetâ‚‚ _ _) where
  obj X := Mâ‚.obj X âŠ— Mâ‚‚.obj X
  map f := tensorObjMap Mâ‚ Mâ‚‚ f
  map_id X := ModuleCat.MonoidalCategory.tensor_ext (by
    intro mâ‚ mâ‚‚
    dsimp [tensorObjMap]
    simp
    rfl) -- `ModuleCat.restrictScalarsId'App_inv_apply` doesn't get picked up due to type mismatch
  map_comp f g := ModuleCat.MonoidalCategory.tensor_ext (by
    intro mâ‚ mâ‚‚
    dsimp [tensorObjMap]
    simp)

variable {Mâ‚ Mâ‚‚ Mâ‚ƒ Mâ‚„}

@[simp]
lemma tensorObj_map_tmul {X Y : Cáµ’áµ–} (f : X âŸ¶ Y) (mâ‚ : Mâ‚.obj X) (mâ‚‚ : Mâ‚‚.obj X) :
    DFunLike.coe (Î± := (Mâ‚.obj X âŠ— Mâ‚‚.obj X :))
      (Î² := fun _ â†¦ (ModuleCat.restrictScalars (R.map f).hom).obj (Mâ‚.obj Y âŠ— Mâ‚‚.obj Y))
      (ModuleCat.Hom.hom (R := â†‘(R.obj X)) ((tensorObj Mâ‚ Mâ‚‚).map f)) (mâ‚ âŠ—â‚œ[R.obj X] mâ‚‚) =
    Mâ‚.map f mâ‚ âŠ—â‚œ[R.obj Y] Mâ‚‚.map f mâ‚‚ := rfl

/-- The tensor product of two morphisms of presheaves of modules. -/
@[simps]
noncomputable def tensorHom (f : Mâ‚ âŸ¶ Mâ‚‚) (g : Mâ‚ƒ âŸ¶ Mâ‚„) : tensorObj Mâ‚ Mâ‚ƒ âŸ¶ tensorObj Mâ‚‚ Mâ‚„ where
  app X := f.app X âŠ— g.app X
  naturality {X Y} Ï† := ModuleCat.MonoidalCategory.tensor_ext (fun mâ‚ mâ‚ƒ â†¦ by
    dsimp
    rw [tensorObj_map_tmul]
    -- Need `erw` because of the type mismatch in `map` and the tensor product.
    erw [ModuleCat.MonoidalCategory.tensorHom_tmul, tensorObj_map_tmul]
    rw [naturality_apply, naturality_apply]
    simp)

end Monoidal

open Monoidal

open ModuleCat.MonoidalCategory in
noncomputable instance monoidalCategoryStruct :
    MonoidalCategoryStruct (PresheafOfModules.{u} (R â‹™ forgetâ‚‚ _ _)) where
  tensorObj := tensorObj
  whiskerLeft _ _ _ g := tensorHom (ðŸ™ _) g
  whiskerRight f _ := tensorHom f (ðŸ™ _)
  tensorHom := tensorHom
  tensorUnit := unit _
  associator Mâ‚ Mâ‚‚ Mâ‚ƒ := isoMk (fun _ â†¦ Î±_ _ _ _)
    (fun _ _ _ â†¦ ModuleCat.MonoidalCategory.tensor_extâ‚ƒ' (by intros; rfl))
  leftUnitor M := Iso.symm (isoMk (fun _ â†¦ (Î»_ _).symm) (fun X Y f â†¦ by
    ext m
    dsimp [CommRingCat.forgetToRingCat_obj]
    erw [leftUnitor_inv_apply, leftUnitor_inv_apply, tensorObj_map_tmul, (R.map f).hom.map_one]
    rfl))
  rightUnitor M := Iso.symm (isoMk (fun _ â†¦ (Ï_ _).symm) (fun X Y f â†¦ by
    ext m
    dsimp [CommRingCat.forgetToRingCat_obj]
    erw [rightUnitor_inv_apply, rightUnitor_inv_apply, tensorObj_map_tmul, (R.map f).hom.map_one]
    rfl))

noncomputable instance monoidalCategory :
    MonoidalCategory (PresheafOfModules.{u} (R â‹™ forgetâ‚‚ _ _)) :=
  ofTensorComp
    (tensorHom_def := fun _ _ => by ext1; apply tensorHom_def)
    (tensor_comp := fun _ _ _ _ => by ext1; apply tensor_comp)
    (whiskerLeft_id := fun Mâ‚ Mâ‚‚ => by
      ext1 X
      apply MonoidalCategory.whiskerLeft_id (C := ModuleCat (R.obj X)))
    (id_whiskerRight := fun _ _ => by
      ext1 X
      apply MonoidalCategory.id_whiskerRight (C := ModuleCat (R.obj X)))
    (associator_naturality := fun _ _ _ => by ext1; apply associator_naturality)
    (leftUnitor_naturality := fun _ => by ext1; apply leftUnitor_naturality)
    (rightUnitor_naturality := fun _ => by ext1; apply rightUnitor_naturality)
    (pentagon := fun _ _ _ _ => by ext1; apply pentagon)
    (triangle := fun _ _ => by ext1; apply triangle)

end PresheafOfModules
