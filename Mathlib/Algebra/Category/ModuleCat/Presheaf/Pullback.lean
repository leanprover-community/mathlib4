/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
module

public import Mathlib.Algebra.Category.ModuleCat.Presheaf.Generator
public import Mathlib.Algebra.Category.ModuleCat.Presheaf.Pushforward
public import Mathlib.CategoryTheory.Adjunction.PartialAdjoint
public import Mathlib.CategoryTheory.Adjunction.CompositionIso

/-!
# Pullback of presheaves of modules

Let `F : C â¥¤ D` be a functor, `R : Dáµ’áµ– â¥¤ RingCat` and `S : Cáµ’áµ– â¥¤ RingCat` be presheaves
of rings, and `Ï† : S âŸ¶ F.op â‹™ R` be a morphism of presheaves of rings,
we introduce the pullback functor `pullback : PresheafOfModules S â¥¤ PresheafOfModules R`
as the left adjoint of `pushforward : PresheafOfModules R â¥¤ PresheafOfModules S`.
The existence of this left adjoint functor is obtained under suitable universe assumptions.

From the compatibility of `pushforward` with respect to composition, we deduce
similar pseudofunctor-like properties of the `pullback` functors.

-/

@[expose] public section

universe v vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ uâ‚ uâ‚‚ uâ‚ƒ uâ‚„ u

open CategoryTheory Limits Opposite Functor

namespace PresheafOfModules

section

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D]
  {F : C â¥¤ D} {R : Dáµ’áµ– â¥¤ RingCat.{u}} {S : Cáµ’áµ– â¥¤ RingCat.{u}} (Ï† : S âŸ¶ F.op â‹™ R)
  [(pushforward.{v} Ï†).IsRightAdjoint]

/-- The pullback functor `PresheafOfModules S â¥¤ PresheafOfModules R` induced by
a morphism of presheaves of rings `S âŸ¶ F.op â‹™ R`, defined as the left adjoint
functor to the pushforward, when it exists. -/
noncomputable def pullback : PresheafOfModules.{v} S â¥¤ PresheafOfModules.{v} R :=
  (pushforward.{v} Ï†).leftAdjoint

/-- Given a morphism of presheaves of rings `S âŸ¶ F.op â‹™ R`, this is the adjunction
between associated pullback and pushforward functors on the categories
of presheaves of modules. -/
noncomputable def pullbackPushforwardAdjunction : pullback.{v} Ï† âŠ£ pushforward.{v} Ï† :=
  Adjunction.ofIsRightAdjoint (pushforward Ï†)

/-- Given a morphism of presheaves of rings `Ï† : S âŸ¶ F.op â‹™ R`, this is the property
that the (partial) left adjoint functor of `pushforward Ï†` is defined
on a certain object `M : PresheafOfModules S`. -/
abbrev pullbackObjIsDefined : ObjectProperty (PresheafOfModules.{v} S) :=
  (pushforward Ï†).leftAdjointObjIsDefined

end

section

variable {C D : Type u} [SmallCategory C] [SmallCategory D]
  {F : C â¥¤ D} {R : Dáµ’áµ– â¥¤ RingCat.{u}} {S : Cáµ’áµ– â¥¤ RingCat.{u}} (Ï† : S âŸ¶ F.op â‹™ R)

/-- Given a morphism of presheaves of rings `Ï† : S âŸ¶ F.op â‹™ R`, where `F : C â¥¤ D`,
`S : Cáµ’áµ– â¥¤ RingCat`, `R : Dáµ’áµ– â¥¤ RingCat` and `X : C`, the (partial) left adjoint
functor of `pushforward Ï†` is defined on the object `(free S).obj (yoneda.obj X)`:
this object shall be mapped to `(free R).obj (yoneda.obj (F.obj X))`. -/
noncomputable def pushforwardCompCoyonedaFreeYonedaCorepresentableBy (X : C) :
    (pushforward Ï† â‹™ coyoneda.obj (op ((free S).obj (yoneda.obj X)))).CorepresentableBy
      ((free R).obj (yoneda.obj (F.obj X))) where
  homEquiv {M} := (freeYonedaEquiv).trans
    (freeYonedaEquiv (M := (pushforward Ï†).obj M)).symm
  homEquiv_comp {M N} g f := freeYonedaEquiv.injective (by
    dsimp
    erw [Equiv.apply_symm_apply, freeYonedaEquiv_comp]
    conv_rhs => erw [freeYonedaEquiv_comp]
    erw [Equiv.apply_symm_apply]
    rfl)

lemma pullbackObjIsDefined_free_yoneda (X : C) :
    pullbackObjIsDefined Ï† ((free S).obj (yoneda.obj X)) :=
  (pushforwardCompCoyonedaFreeYonedaCorepresentableBy Ï† X).isCorepresentable

lemma pullbackObjIsDefined_eq_top :
    pullbackObjIsDefined.{u} Ï† = âŠ¤ := by
  ext M
  simp only [Pi.top_apply, Prop.top_eq_true, iff_true]
  apply leftAdjointObjIsDefined_of_isColimit
    M.isColimitFreeYonedaCoproductsCokernelCofork
  rintro (_ | _)
  all_goals
    apply leftAdjointObjIsDefined_colimit _
      (fun _ â†¦ pullbackObjIsDefined_free_yoneda _ _)

instance : (pushforward.{u} Ï†).IsRightAdjoint :=
  isRightAdjoint_of_leftAdjointObjIsDefined_eq_top
    (pullbackObjIsDefined_eq_top Ï†)

end

section

variable {C : Type uâ‚} [Category.{vâ‚} C] {D : Type uâ‚‚} [Category.{vâ‚‚} D]
  {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E] {E' : Type uâ‚„} [Category.{vâ‚„} E']

variable {F : C â¥¤ D} {R : Dáµ’áµ– â¥¤ RingCat.{u}} {S : Cáµ’áµ– â¥¤ RingCat.{u}} (Ï† : S âŸ¶ F.op â‹™ R)
  {G : D â¥¤ E} {T : Eáµ’áµ– â¥¤ RingCat.{u}} (Ïˆ : R âŸ¶ G.op â‹™ T)

instance : (pushforward.{v} (F := ğŸ­ C) (ğŸ™ S)).IsRightAdjoint :=
  isRightAdjoint_of_iso (pushforwardId.{v} S).symm

variable (S) in
/-- The pullback by the identity morphism identifies to the identity functor of the
category of presheaves of modules. -/
noncomputable def pullbackId : pullback.{v} (F := ğŸ­ C) (ğŸ™ S) â‰… ğŸ­ _ :=
  ((pullbackPushforwardAdjunction.{v} (F := ğŸ­ C) (ğŸ™ S))).leftAdjointIdIso (pushforwardId S)

variable [(pushforward.{v} Ï†).IsRightAdjoint]

section

variable [(pushforward.{v} Ïˆ).IsRightAdjoint]

instance : (pushforward.{v} (F := F â‹™ G) (Ï† â‰« whiskerLeft F.op Ïˆ)).IsRightAdjoint :=
  isRightAdjoint_of_iso (pushforwardComp.{v} Ï† Ïˆ)

/-- The composition of two pullback functors on presheaves of modules identifies
to the pullback for the composition. -/
noncomputable def pullbackComp :
    pullback.{v} Ï† â‹™ pullback.{v} Ïˆ â‰…
      pullback.{v} (F := F â‹™ G) (Ï† â‰« whiskerLeft F.op Ïˆ) :=
  Adjunction.leftAdjointCompIso
    (pullbackPushforwardAdjunction.{v} Ï†) (pullbackPushforwardAdjunction.{v} Ïˆ)
    (pullbackPushforwardAdjunction.{v} (F := F â‹™ G) (Ï† â‰« whiskerLeft F.op Ïˆ))
    (pushforwardComp Ï† Ïˆ)

variable {T' : E'áµ’áµ– â¥¤ RingCat.{u}} {G' : E â¥¤ E'} (Ïˆ' : T âŸ¶ G'.op â‹™ T')
  [(pushforward.{v} Ïˆ').IsRightAdjoint]

lemma pullback_assoc :
    isoWhiskerLeft _ (pullbackComp.{v} Ïˆ Ïˆ') â‰ªâ‰«
      pullbackComp.{v} (G := G â‹™ G') Ï† (Ïˆ â‰« whiskerLeft G.op Ïˆ') =
    (associator _ _ _).symm â‰ªâ‰« isoWhiskerRight (pullbackComp.{v} Ï† Ïˆ) _ â‰ªâ‰«
        pullbackComp.{v} (F := F â‹™ G) (Ï† â‰« whiskerLeft F.op Ïˆ) Ïˆ' :=
  Adjunction.leftAdjointCompIso_assoc _ _ _ _ _ _ _ _ _ _ (pushforward_assoc Ï† Ïˆ Ïˆ')

end

lemma pullback_id_comp :
    pullbackComp.{v} (F := ğŸ­ C) (ğŸ™ S) Ï† =
      isoWhiskerRight (pullbackId S) (pullback Ï†) â‰ªâ‰« Functor.leftUnitor _ :=
  Adjunction.leftAdjointCompIso_id_comp _ _ _ _ (pushforward_comp_id Ï†)

lemma pullback_comp_id :
    pullbackComp.{v} (G := ğŸ­ _) Ï† (ğŸ™ R) =
      isoWhiskerLeft _ (pullbackId R) â‰ªâ‰« Functor.rightUnitor _ :=
  Adjunction.leftAdjointCompIso_comp_id _ _ _ _ (pushforward_id_comp Ï†)

end

end PresheafOfModules
