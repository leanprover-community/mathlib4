/-
Copyright (c) 2025 Ya√´l Dillies, Christian Merten, Micha≈Ç Mruga≈Ça, Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Christian Merten, Micha≈Ç Mruga≈Ça, Andrew Yang
-/
import Mathlib.Algebra.Category.CommAlgCat.Basic
import Mathlib.CategoryTheory.Monoidal.Cartesian.Basic

/-!
# The co-Cartesian monoidal category structure on commutative `R`-algebras

This file provides the co-Cartesian-monoidal category structure on `CommAlgCat R` constructed
explicitly using the tensor product.
-/

open CategoryTheory MonoidalCategory CartesianMonoidalCategory Limits TensorProduct Opposite
open Algebra.TensorProduct
open Algebra.TensorProduct (lid rid assoc comm)

noncomputable section

namespace CommAlgCat
universe u v
variable {R : Type u} [CommRing R] {A B C D : CommAlgCat.{u} R}

variable (A B)

/-- The explicit cocone with tensor products as the fibered coproduct in `CommAlgCat`. -/
def binaryCofan : BinaryCofan A B := .mk (ofHom includeLeft) (ofHom <| includeRight (A := A))

@[simp] lemma binaryCofan_inl : (binaryCofan A B).inl = ofHom includeLeft := rfl
@[simp] lemma binaryCofan_inr : (binaryCofan A B).inr = ofHom includeRight := rfl
@[simp] lemma binaryCofan_pt : (binaryCofan A B).pt = .of R (A ‚äó[R] B) := rfl

/-- Verify that the pushout cocone is indeed the colimit. -/
def binaryCofanIsColimit : IsColimit (binaryCofan A B) :=
  BinaryCofan.IsColimit.mk _
    (fun f g ‚Ü¶ ofHom (lift f.hom g.hom fun _ _ ‚Ü¶ .all _ _))
    (fun f g ‚Ü¶ by ext1; exact lift_comp_includeLeft _ _ fun _ _ ‚Ü¶ .all _ _)
    (fun f g ‚Ü¶ by ext1; exact lift_comp_includeRight _ _ fun _ _ ‚Ü¶ .all _ _)
    (fun f g m hm‚ÇÅ hm‚ÇÇ ‚Ü¶ by
      ext1
      refine liftEquiv.symm_apply_eq (y := ‚ü®‚ü®_, _‚ü©, fun _ _ ‚Ü¶ .all _ _‚ü©).mp ?_
      exact Subtype.ext (Prod.ext congr(($hm‚ÇÅ).hom) congr(($hm‚ÇÇ).hom)))

/-- The initial object of `CommAlgCat R` is `R` as an algebra over itself. -/
def isInitialSelf : IsInitial (of R R) :=
  .ofUniqueHom (fun A ‚Ü¶ ofHom (Algebra.ofId R A)) fun _ _ ‚Ü¶ hom_ext (Algebra.ext_id _ _ _)

attribute [local simp] one_def in
instance : MonoidalCategory (CommAlgCat.{u} R) where
  tensorObj S T := of R (S ‚äó[R] T)
  whiskerLeft _ {_ _} f := ofHom (map (.id _ _) f.hom)
  whiskerRight f T := ofHom (map f.hom (.id _ _))
  tensorHom f g := ofHom (map f.hom g.hom)
  tensorUnit := .of R R
  associator _ _ _ := isoMk (assoc R R _ _ _)
  leftUnitor _ := isoMk (lid R _)
  rightUnitor _ := isoMk (rid R R _)

@[simp] lemma coe_tensorUnit : ùüô_ (CommAlgCat.{u} R) = R := rfl

@[simp] lemma coe_tensorObj : A ‚äó B = A ‚äó[R] B := rfl

variable {A B}

@[simp] lemma tensorHom_hom (f : A ‚ü∂ C) (g : B ‚ü∂ D) : (f ‚äó‚Çò g).hom = map f.hom g.hom := rfl

variable (C) in
@[simp] lemma whiskerRight_hom (f : A ‚ü∂ B) : (f ‚ñ∑ C).hom = map f.hom (.id _ _) := rfl

variable (C) in
@[simp] lemma whiskerLeft_hom (f : A ‚ü∂ B) : (C ‚óÅ f).hom = map (.id _ _) f.hom := rfl

variable (A B C) in
@[simp] lemma associator_hom_hom : (Œ±_ A B C).hom.hom = (assoc R R A B C).toAlgHom := rfl

variable (A B C) in
@[simp] lemma associator_inv_hom : (Œ±_ A B C).inv.hom = (assoc R R A B C).symm.toAlgHom := rfl

instance : BraidedCategory (CommAlgCat.{u} R) where
  braiding S T := isoMk (comm R _ _)
  braiding_naturality_right := by intros; ext : 1; dsimp; ext <;> rfl
  braiding_naturality_left := by intros; ext : 1; dsimp; ext <;> rfl
  hexagon_forward S T U := by ext : 1; dsimp; ext <;> rfl
  hexagon_reverse S T U := by ext : 1; dsimp; ext <;> rfl

variable (A B) in
@[simp] lemma braiding_hom_hom : (Œ≤_ A B).hom.hom = (comm R A B).toAlgHom := rfl

variable (A B) in
@[simp] lemma braiding_inv_hom : (Œ≤_ A B).inv.hom = (comm R B A).toAlgHom := rfl

attribute [local ext] Quiver.Hom.unop_inj in
instance : CartesianMonoidalCategory (CommAlgCat.{u} R)·µí·µñ where
  isTerminalTensorUnit := terminalOpOfInitial isInitialSelf
  fst := _
  snd := _
  tensorProductIsBinaryProduct S T := BinaryCofan.IsColimit.op <| binaryCofanIsColimit S.unop T.unop
  fst_def S T := by ext x; change x ‚äó‚Çú 1 = x ‚äó‚Çú algebraMap R T.unop 1; simp
  snd_def S T := by ext x; change 1 ‚äó‚Çú x = algebraMap R S.unop 1 ‚äó‚Çú x; simp

variable {A B C D : (CommAlgCat.{u} R)·µí·µñ}

@[simp] lemma fst_unop_hom (A B : (CommAlgCat.{u} R)·µí·µñ) : (fst A B).unop.hom = includeLeft := rfl
@[simp] lemma snd_unop_hom (A B : (CommAlgCat.{u} R)·µí·µñ) : (snd A B).unop.hom = includeRight := rfl

variable (A B) in
@[simp] lemma toUnit_unop_hom : (toUnit A).unop.hom = Algebra.ofId R A.unop := rfl

@[simp] lemma lift_unop_hom (f : C ‚ü∂ A) (g : C ‚ü∂ B) :
    (lift f g).unop.hom = lift f.unop.hom g.unop.hom fun _ _ ‚Ü¶ .all _ _ := rfl

end CommAlgCat
