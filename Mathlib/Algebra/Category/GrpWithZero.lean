/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Algebra.Category.MonCat.Basic
import Mathlib.Algebra.GroupWithZero.WithZero
import Mathlib.CategoryTheory.Category.Bipointed

/-!
# The category of groups with zero

This file defines `GrpWithZero`, the category of groups with zero.
-/

assert_not_exists Ring

universe u

open CategoryTheory

/-- The category of groups with zero. -/
structure GrpWithZero where
  /-- The underlying group with zero. -/
  carrier : Type*
  [str : GroupWithZero carrier]

attribute [instance] GrpWithZero.str

namespace GrpWithZero

instance : CoeSort GrpWithZero Type* :=
  âŸ¨carrierâŸ©

/-- Construct a bundled `GrpWithZero` from a `GroupWithZero`. -/
abbrev of (Î± : Type*) [GroupWithZero Î±] : GrpWithZero where
  carrier := Î±

instance : Inhabited GrpWithZero :=
  âŸ¨of (WithZero PUnit)âŸ©

instance : LargeCategory.{u} GrpWithZero where
  Hom X Y := MonoidWithZeroHom X Y
  id X := MonoidWithZeroHom.id X
  comp f g := g.comp f

instance groupWithZeroConcreteCategory : ConcreteCategory GrpWithZero (MonoidWithZeroHom Â· Â·) where
  hom f := f
  ofHom f := f

/-- Typecheck a `MonoidWithZeroHom` as a morphism in `GrpWithZero`. -/
abbrev ofHom {X Y : Type u} [GroupWithZero X] [GroupWithZero Y]
    (f : MonoidWithZeroHom X Y) : of X âŸ¶ of Y :=
  ConcreteCategory.ofHom f

@[simp]
lemma hom_id {X : GrpWithZero} : ConcreteCategory.hom (ðŸ™ X : X âŸ¶ X) = MonoidWithZeroHom.id X := rfl

@[simp]
lemma hom_comp {X Y Z : GrpWithZero} {f : X âŸ¶ Y} {g : Y âŸ¶ Z} :
    ConcreteCategory.hom (f â‰« g) = g.comp f := rfl

lemma coe_id {X : GrpWithZero} : (ðŸ™ X : X â†’ X) = id := rfl

lemma coe_comp {X Y Z : GrpWithZero} {f : X âŸ¶ Y} {g : Y âŸ¶ Z} : (f â‰« g : X â†’ Z) = g âˆ˜ f := rfl

@[simp] lemma forget_map {X Y : GrpWithZero} (f : X âŸ¶ Y) : (forget GrpWithZero).map f = f := rfl

instance hasForgetToBipointed : HasForgetâ‚‚ GrpWithZero Bipointed where
  forgetâ‚‚ :=
      { obj := fun X => âŸ¨X, 0, 1âŸ©
        map := fun f => âŸ¨f, f.map_zero', f.map_one'âŸ© }

instance hasForgetToMon : HasForgetâ‚‚ GrpWithZero MonCat where
  forgetâ‚‚ :=
      { obj := fun X => MonCat.of X
        map := fun f => MonCat.ofHom f.toMonoidHom }

/-- Constructs an isomorphism of groups with zero from a group isomorphism between them. -/
@[simps]
def Iso.mk {Î± Î² : GrpWithZero.{u}} (e : Î± â‰ƒ* Î²) : Î± â‰… Î² where
  hom := ofHom e
  inv := ofHom e.symm
  hom_inv_id := by
    ext
    exact e.symm_apply_apply _
  inv_hom_id := by
    ext
    exact e.apply_symm_apply _

end GrpWithZero
