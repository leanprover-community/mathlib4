/-
Copyright (c) 2022 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies
-/
import Mathlib.Algebra.Category.MonCat.Basic
import Mathlib.Algebra.GroupWithZero.WithZero
import Mathlib.CategoryTheory.Category.Bipointed

/-!
# The category of groups with zero

This file defines `GrpWithZero`, the category of groups with zero.
-/

assert_not_exists Ring

universe u

open CategoryTheory

/-- The category of groups with zero. -/
structure GrpWithZero where
  /-- The underlying group with zero. -/
  carrier : Type*
  [str : GroupWithZero carrier]

attribute [instance] GrpWithZero.str

namespace GrpWithZero

instance : CoeSort GrpWithZero Type* :=
  ‚ü®carrier‚ü©

/-- Construct a bundled `GrpWithZero` from a `GroupWithZero`. -/
abbrev of (Œ± : Type*) [GroupWithZero Œ±] : GrpWithZero where
  carrier := Œ±

instance : Inhabited GrpWithZero :=
  ‚ü®of (WithZero PUnit)‚ü©

instance : LargeCategory.{u} GrpWithZero where
  Hom X Y := MonoidWithZeroHom X Y
  id X := MonoidWithZeroHom.id X
  comp f g := g.comp f

instance groupWithZeroConcreteCategory : ConcreteCategory GrpWithZero (MonoidWithZeroHom ¬∑ ¬∑) where
  hom f := f
  ofHom f := f

/-- Typecheck a `MonoidWithZeroHom` as a morphism in `GrpWithZero`. -/
abbrev ofHom {X Y : Type u} [GroupWithZero X] [GroupWithZero Y]
    (f : MonoidWithZeroHom X Y) : of X ‚ü∂ of Y :=
  ConcreteCategory.ofHom f

@[simp]
lemma hom_id {X : GrpWithZero} : ConcreteCategory.hom (ùüô X : X ‚ü∂ X) = MonoidWithZeroHom.id X := rfl

@[simp]
lemma hom_comp {X Y Z : GrpWithZero} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z} :
    ConcreteCategory.hom (f ‚â´ g) = g.comp f := rfl

lemma coe_id {X : GrpWithZero} : (ùüô X : X ‚Üí X) = id := rfl

lemma coe_comp {X Y Z : GrpWithZero} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z} : (f ‚â´ g : X ‚Üí Z) = g ‚àò f := rfl

@[simp] lemma forget_map {X Y : GrpWithZero} (f : X ‚ü∂ Y) : (forget GrpWithZero).map f = f := rfl

instance hasForgetToBipointed : HasForget‚ÇÇ GrpWithZero Bipointed where
  forget‚ÇÇ :=
      { obj := fun X ‚Ü¶ ‚ü®X, 0, 1‚ü©
        map := fun f ‚Ü¶ ‚ü®f, f.map_zero', f.map_one'‚ü© }

instance hasForgetToMon : HasForget‚ÇÇ GrpWithZero MonCat where
  forget‚ÇÇ :=
      { obj := fun X ‚Ü¶ MonCat.of X
        map := fun f ‚Ü¶ MonCat.ofHom f.toMonoidHom }

/-- Constructs an isomorphism of groups with zero from a group isomorphism between them. -/
@[simps]
def Iso.mk {Œ± Œ≤ : GrpWithZero.{u}} (e : Œ± ‚âÉ* Œ≤) : Œ± ‚âÖ Œ≤ where
  hom := ofHom e
  inv := ofHom e.symm
  hom_inv_id := by
    ext
    exact e.symm_apply_apply _
  inv_hom_id := by
    ext
    exact e.apply_symm_apply _

end GrpWithZero
