/-
Copyright (c) 2023 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.CategoryTheory.Monoidal.Transport
import Mathlib.Algebra.Category.AlgebraCat.Basic
import Mathlib.Algebra.Category.ModuleCat.Monoidal.Basic
import Mathlib.RingTheory.TensorProduct.Basic

/-!
# The monoidal category structure on R-algebras
-/

open CategoryTheory
open scoped MonoidalCategory

universe v u

variable {R : Type u} [CommRing R]

namespace AlgebraCat

noncomputable section

namespace instMonoidalCategory

open scoped TensorProduct

/-- Auxiliary definition used to fight a timeout when building
`AlgebraCat.instMonoidalCategory`. -/
@[simps!]
noncomputable abbrev tensorObj (X Y : AlgebraCat.{u} R) : AlgebraCat.{u} R :=
  of R (X âŠ—[R] Y)

/-- Auxiliary definition used to fight a timeout when building
`AlgebraCat.instMonoidalCategory`. -/
noncomputable abbrev tensorHom {W X Y Z : AlgebraCat.{u} R} (f : W âŸ¶ X) (g : Y âŸ¶ Z) :
    tensorObj W Y âŸ¶ tensorObj X Z :=
  Algebra.TensorProduct.map f g

open MonoidalCategory

end instMonoidalCategory

open instMonoidalCategory

instance : MonoidalCategoryStruct (AlgebraCat.{u} R) where
  tensorObj := instMonoidalCategory.tensorObj
  whiskerLeft X _ _ f := tensorHom (ðŸ™ X) f
  whiskerRight {Xâ‚ Xâ‚‚} (f : Xâ‚ âŸ¶ Xâ‚‚) Y := tensorHom f (ðŸ™ Y)
  tensorHom := tensorHom
  tensorUnit := of R R
  associator X Y Z := (Algebra.TensorProduct.assoc R X Y Z).toAlgebraIso
  leftUnitor X := (Algebra.TensorProduct.lid R X).toAlgebraIso
  rightUnitor X := (Algebra.TensorProduct.rid R R X).toAlgebraIso

noncomputable instance instMonoidalCategory : MonoidalCategory (AlgebraCat.{u} R) :=
  Monoidal.induced
    (forgetâ‚‚ (AlgebraCat R) (ModuleCat R))
    { Î¼Iso := fun _ _ => Iso.refl _
      ÎµIso := Iso.refl _
      associator_eq := fun _ _ _ => TensorProduct.ext_threefold (fun _ _ _ => rfl)
      leftUnitor_eq := fun _ => TensorProduct.ext' (fun _ _ => rfl)
      rightUnitor_eq := fun _ => TensorProduct.ext' (fun _ _ => rfl) }

variable (R) in
/-- `forgetâ‚‚ (AlgebraCat R) (ModuleCat R)` as a monoidal functor. -/
def toModuleCatMonoidalFunctor : MonoidalFunctor (AlgebraCat.{u} R) (ModuleCat.{u} R) := by
  unfold instMonoidalCategory
  exact Monoidal.fromInduced (forgetâ‚‚ (AlgebraCat R) (ModuleCat R)) _

instance : (toModuleCatMonoidalFunctor R).Faithful :=
  forgetâ‚‚_faithful _ _

end

end AlgebraCat
