/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Algebra.Algebra.Subalgebra.Basic
import Mathlib.Algebra.FreeAlgebra
import Mathlib.Algebra.Category.Ring.Basic
import Mathlib.Algebra.Category.ModuleCat.Basic

#align_import algebra.category.Algebra.basic from "leanprover-community/mathlib"@"79ffb5563b56fefdea3d60b5736dad168a9494ab"

/-!
# Category instance for algebras over a commutative ring

We introduce the bundled category `AlgebraCat` of algebras over a fixed commutative ring `R` along
with the forgetful functors to `RingCat` and `ModuleCat`. We furthermore show that the functor
associating to a type the free `R`-algebra on that type is left adjoint to the forgetful functor.
-/

set_option linter.uppercaseLean3 false

open CategoryTheory

open CategoryTheory.Limits

universe v u

variable (R : Type u) [CommRing R]

/-- The category of R-algebras and their morphisms. -/
structure AlgebraCat where
  carrier : Type v
  [isRing : Ring carrier]
  [isAlgebra : Algebra R carrier]
#align Algebra AlgebraCat

-- Porting note: typemax hack to fix universe complaints
/-- An alias for `AlgebraCat.{max u‚ÇÅ u‚ÇÇ}`, to deal around unification issues.
Since the universe the ring lives in can be inferred, we put that last. -/
@[nolint checkUnivs]
abbrev AlgebraCatMax.{v‚ÇÅ, v‚ÇÇ, u‚ÇÅ} (R : Type u‚ÇÅ) [CommRing R] := AlgebraCat.{max v‚ÇÅ v‚ÇÇ} R

attribute [instance] AlgebraCat.isRing AlgebraCat.isAlgebra

initialize_simps_projections AlgebraCat (-isRing, -isAlgebra)

namespace AlgebraCat

instance : CoeSort (AlgebraCat R) (Type v) :=
  ‚ü®AlgebraCat.carrier‚ü©

attribute [coe] AlgebraCat.carrier

instance : Category (AlgebraCat.{v} R) where
  Hom A B := A ‚Üí‚Çê[R] B
  id A := AlgHom.id R A
  comp f g := g.comp f

instance {M N : AlgebraCat.{v} R} : FunLike (M ‚ü∂ N) M N :=
  AlgHom.funLike

instance {M N : AlgebraCat.{v} R} : AlgHomClass (M ‚ü∂ N) R M N :=
  AlgHom.algHomClass

instance : ConcreteCategory.{v} (AlgebraCat.{v} R) where
  forget :=
    { obj := fun R => R
      map := fun f => f.toFun }
  forget_faithful := ‚ü®fun h => AlgHom.ext (by intros x; dsimp at h; rw [h])‚ü©

instance {S : AlgebraCat.{v} R} : Ring ((forget (AlgebraCat R)).obj S) :=
  (inferInstance : Ring S.carrier)

instance {S : AlgebraCat.{v} R} : Algebra R ((forget (AlgebraCat R)).obj S) :=
  (inferInstance : Algebra R S.carrier)

instance hasForgetToRing : HasForget‚ÇÇ (AlgebraCat.{v} R) RingCat.{v} where
  forget‚ÇÇ :=
    { obj := fun A => RingCat.of A
      map := fun f => RingCat.ofHom f.toRingHom }
#align Algebra.has_forget_to_Ring AlgebraCat.hasForgetToRing

instance hasForgetToModule : HasForget‚ÇÇ (AlgebraCat.{v} R) (ModuleCat.{v} R) where
  forget‚ÇÇ :=
    { obj := fun M => ModuleCat.of R M
      map := fun f => ModuleCat.ofHom f.toLinearMap }
#align Algebra.has_forget_to_Module AlgebraCat.hasForgetToModule

@[simp]
lemma forget‚ÇÇ_module_obj (X : AlgebraCat.{v} R) :
    (forget‚ÇÇ (AlgebraCat.{v} R) (ModuleCat.{v} R)).obj X = ModuleCat.of R X :=
  rfl

@[simp]
lemma forget‚ÇÇ_module_map {X Y : AlgebraCat.{v} R} (f : X ‚ü∂ Y) :
    (forget‚ÇÇ (AlgebraCat.{v} R) (ModuleCat.{v} R)).map f = ModuleCat.ofHom f.toLinearMap :=
  rfl

/-- The object in the category of R-algebras associated to a type equipped with the appropriate
typeclasses. -/
def of (X : Type v) [Ring X] [Algebra R X] : AlgebraCat.{v} R :=
  ‚ü®X‚ü©
#align Algebra.of AlgebraCat.of

/-- Typecheck a `AlgHom` as a morphism in `AlgebraCat R`. -/
def ofHom {R : Type u} [CommRing R] {X Y : Type v} [Ring X] [Algebra R X] [Ring Y] [Algebra R Y]
    (f : X ‚Üí‚Çê[R] Y) : of R X ‚ü∂ of R Y :=
  f
#align Algebra.of_hom AlgebraCat.ofHom

@[simp]
theorem ofHom_apply {R : Type u} [CommRing R] {X Y : Type v} [Ring X] [Algebra R X] [Ring Y]
    [Algebra R Y] (f : X ‚Üí‚Çê[R] Y) (x : X) : ofHom f x = f x :=
  rfl
#align Algebra.of_hom_apply AlgebraCat.ofHom_apply

instance : Inhabited (AlgebraCat R) :=
  ‚ü®of R R‚ü©

@[simp]
theorem coe_of (X : Type u) [Ring X] [Algebra R X] : (of R X : Type u) = X :=
  rfl
#align Algebra.coe_of AlgebraCat.coe_of

variable {R}

/-- Forgetting to the underlying type and then building the bundled object returns the original
algebra. -/
@[simps]
def ofSelfIso (M : AlgebraCat.{v} R) : AlgebraCat.of R M ‚âÖ M where
  hom := ùüô M
  inv := ùüô M
#align Algebra.of_self_iso AlgebraCat.ofSelfIso

variable {M N U : ModuleCat.{v} R}

@[simp]
theorem id_apply (m : M) : (ùüô M : M ‚Üí M) m = m :=
  rfl
#align Algebra.id_apply AlgebraCat.id_apply

@[simp]
theorem coe_comp (f : M ‚ü∂ N) (g : N ‚ü∂ U) : (f ‚â´ g : M ‚Üí U) = g ‚àò f :=
  rfl
#align Algebra.coe_comp AlgebraCat.coe_comp

variable (R)

/-- The "free algebra" functor, sending a type `S` to the free algebra on `S`. -/
@[simps!]
def free : Type u ‚•§ AlgebraCat.{u} R where
  obj S :=
    { carrier := FreeAlgebra R S
      isRing := Algebra.semiringToRing R }
  map f := FreeAlgebra.lift _ <| FreeAlgebra.Œπ _ ‚àò f
  -- Porting note: `apply FreeAlgebra.hom_ext` was `ext1`.
  map_id := by intro X; apply FreeAlgebra.hom_ext; simp only [FreeAlgebra.Œπ_comp_lift]; rfl
  map_comp := by
  -- Porting note: `apply FreeAlgebra.hom_ext` was `ext1`.
    intros; apply FreeAlgebra.hom_ext; simp only [FreeAlgebra.Œπ_comp_lift]; ext1
    -- Porting node: this ‚Üì `erw` used to be handled by the `simp` below it
    erw [CategoryTheory.coe_comp]
    simp only [CategoryTheory.coe_comp, Function.comp_apply, types_comp_apply]
    -- Porting node: this ‚Üì `erw` and `rfl` used to be handled by the `simp` above
    erw [FreeAlgebra.lift_Œπ_apply, FreeAlgebra.lift_Œπ_apply]
    rfl
#align Algebra.free AlgebraCat.free

/-- The free/forget adjunction for `R`-algebras. -/
def adj : free.{u} R ‚ä£ forget (AlgebraCat.{u} R) :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun X A => (FreeAlgebra.lift _).symm
      -- Relying on `obviously` to fill out these proofs is very slow :(
      homEquiv_naturality_left_symm := by
        -- Porting note: `apply FreeAlgebra.hom_ext` was `ext1`.
        intros; apply FreeAlgebra.hom_ext; simp only [FreeAlgebra.Œπ_comp_lift]; ext1
        simp only [free_map, Equiv.symm_symm, FreeAlgebra.lift_Œπ_apply, CategoryTheory.coe_comp,
          Function.comp_apply, types_comp_apply]
        -- Porting node: this ‚Üì `erw` and `rfl` used to be handled by the `simp` above
        erw [FreeAlgebra.lift_Œπ_apply, CategoryTheory.comp_apply, FreeAlgebra.lift_Œπ_apply,
          Function.comp_apply, FreeAlgebra.lift_Œπ_apply]
        rfl
      homEquiv_naturality_right := by
        intros; ext
        simp only [CategoryTheory.coe_comp, Function.comp_apply,
          FreeAlgebra.lift_symm_apply, types_comp_apply]
        -- Porting note: proof used to be done after this ‚Üë `simp`; added ‚Üì two lines
        erw [FreeAlgebra.lift_symm_apply, FreeAlgebra.lift_symm_apply]
        rfl }
#align Algebra.adj AlgebraCat.adj

instance : (forget (AlgebraCat.{u} R)).IsRightAdjoint := (adj R).isRightAdjoint

end AlgebraCat

variable {R}
variable {X‚ÇÅ X‚ÇÇ : Type u}

/-- Build an isomorphism in the category `AlgebraCat R` from a `AlgEquiv` between `Algebra`s. -/
@[simps]
def AlgEquiv.toAlgebraIso {g‚ÇÅ : Ring X‚ÇÅ} {g‚ÇÇ : Ring X‚ÇÇ} {m‚ÇÅ : Algebra R X‚ÇÅ} {m‚ÇÇ : Algebra R X‚ÇÇ}
    (e : X‚ÇÅ ‚âÉ‚Çê[R] X‚ÇÇ) : AlgebraCat.of R X‚ÇÅ ‚âÖ AlgebraCat.of R X‚ÇÇ where
  hom := (e : X‚ÇÅ ‚Üí‚Çê[R] X‚ÇÇ)
  inv := (e.symm : X‚ÇÇ ‚Üí‚Çê[R] X‚ÇÅ)
  hom_inv_id := by ext x; exact e.left_inv x
  inv_hom_id := by ext x; exact e.right_inv x
#align alg_equiv.to_Algebra_iso AlgEquiv.toAlgebraIso

namespace CategoryTheory.Iso

/-- Build a `AlgEquiv` from an isomorphism in the category `AlgebraCat R`. -/
@[simps]
def toAlgEquiv {X Y : AlgebraCat R} (i : X ‚âÖ Y) : X ‚âÉ‚Çê[R] Y where
  toFun := i.hom
  invFun := i.inv
  left_inv x := by
    -- Porting note: was `by tidy`
    change (i.hom ‚â´ i.inv) x = x
    simp only [hom_inv_id]
    -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
    erw [id_apply]
  right_inv x := by
    -- Porting note: was `by tidy`
    change (i.inv ‚â´ i.hom) x = x
    simp only [inv_hom_id]
    -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
    erw [id_apply]
  map_add' := by aesop
  map_mul' := by aesop
  commutes' := i.hom.commutes -- Porting note: was `by tidy`
#align category_theory.iso.to_alg_equiv CategoryTheory.Iso.toAlgEquiv

end CategoryTheory.Iso

/-- Algebra equivalences between `Algebra`s are the same as (isomorphic to) isomorphisms in
`AlgebraCat`. -/
@[simps]
def algEquivIsoAlgebraIso {X Y : Type u} [Ring X] [Ring Y] [Algebra R X] [Algebra R Y] :
    (X ‚âÉ‚Çê[R] Y) ‚âÖ AlgebraCat.of R X ‚âÖ AlgebraCat.of R Y where
  hom e := e.toAlgebraIso
  inv i := i.toAlgEquiv
#align alg_equiv_iso_Algebra_iso algEquivIsoAlgebraIso

-- Porting note: changed to `CoeOut`
instance (X : Type u) [Ring X] [Algebra R X] : CoeOut (Subalgebra R X) (AlgebraCat R) :=
  ‚ü®fun N => AlgebraCat.of R N‚ü©

instance AlgebraCat.forget_reflects_isos : (forget (AlgebraCat.{u} R)).ReflectsIsomorphisms where
  reflects {X Y} f _ := by
    let i := asIso ((forget (AlgebraCat.{u} R)).map f)
    let e : X ‚âÉ‚Çê[R] Y := { f, i.toEquiv with }
    exact e.toAlgebraIso.isIso_hom
#align Algebra.forget_reflects_isos AlgebraCat.forget_reflects_isos

/-!
`@[simp]` lemmas for `AlgHom.comp` and categorical identities.
-/

@[simp] theorem AlgHom.comp_id_algebraCat
    {R} [CommRing R] {G : AlgebraCat.{u} R} {H : Type u} [Ring H] [Algebra R H] (f : G ‚Üí‚Çê[R] H) :
    f.comp (ùüô G) = f :=
  Category.id_comp (AlgebraCat.ofHom f)
@[simp] theorem AlgHom.id_algebraCat_comp
    {R} [CommRing R] {G : Type u} [Ring G] [Algebra R G] {H : AlgebraCat.{u} R} (f : G ‚Üí‚Çê[R] H) :
    AlgHom.comp (ùüô H) f = f :=
  Category.comp_id (AlgebraCat.ofHom f)
