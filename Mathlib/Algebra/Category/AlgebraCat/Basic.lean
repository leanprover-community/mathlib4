/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.Algebra.Algebra.Subalgebra.Basic
import Mathlib.Algebra.FreeAlgebra
import Mathlib.Algebra.Category.Ring.Basic
import Mathlib.Algebra.Category.ModuleCat.Basic

/-!
# Category instance for algebras over a commutative ring

We introduce the bundled category `AlgebraCat` of algebras over a fixed commutative ring `R` along
with the forgetful functors to `RingCat` and `ModuleCat`. We furthermore show that the functor
associating to a type the free `R`-algebra on that type is left adjoint to the forgetful functor.
-/

open CategoryTheory Limits

universe v u

variable (R : Type u) [CommRing R]

/-- The category of R-algebras and their morphisms. -/
structure AlgebraCat where
  private mk ::
  /-- The underlying type. -/
  carrier : Type v
  [isRing : Ring carrier]
  [isAlgebra : Algebra R carrier]

attribute [instance] AlgebraCat.isRing AlgebraCat.isAlgebra

initialize_simps_projections AlgebraCat (-isRing, -isAlgebra)

namespace AlgebraCat

instance : CoeSort (AlgebraCat R) (Type v) :=
  ‚ü®AlgebraCat.carrier‚ü©

attribute [coe] AlgebraCat.carrier

/-- The object in the category of R-algebras associated to a type equipped with the appropriate
typeclasses. This is the preferred way to construct a term of `AlgebraCat R`. -/
abbrev of (X : Type v) [Ring X] [Algebra R X] : AlgebraCat.{v} R :=
  ‚ü®X‚ü©

lemma coe_of (X : Type v) [Ring X] [Algebra R X] : (of R X : Type v) = X :=
  rfl

variable {R} in
/-- The type of morphisms in `AlgebraCat R`. -/
@[ext]
structure Hom (A B : AlgebraCat.{v} R) where
  private mk ::
  /-- The underlying algebra map. -/
  hom' : A ‚Üí‚Çê[R] B

instance : Category (AlgebraCat.{v} R) where
  Hom A B := Hom A B
  id A := ‚ü®AlgHom.id R A‚ü©
  comp f g := ‚ü®g.hom'.comp f.hom'‚ü©

instance : ConcreteCategory (AlgebraCat.{v} R) (¬∑ ‚Üí‚Çê[R] ¬∑) where
  hom := Hom.hom'
  ofHom := Hom.mk

variable {R} in
/-- Turn a morphism in `AlgebraCat` back into an `AlgHom`. -/
abbrev Hom.hom {A B : AlgebraCat.{v} R} (f : Hom A B) :=
  ConcreteCategory.hom (C := AlgebraCat R) f

variable {R} in
/-- Typecheck an `AlgHom` as a morphism in `AlgebraCat`. -/
abbrev ofHom {A B : Type v} [Ring A] [Ring B] [Algebra R A] [Algebra R B] (f : A ‚Üí‚Çê[R] B) :
    of R A ‚ü∂ of R B :=
  ConcreteCategory.ofHom (C := AlgebraCat R) f

variable {R} in
/-- Use the `ConcreteCategory.hom` projection for `@[simps]` lemmas. -/
def Hom.Simps.hom (A B : AlgebraCat.{v} R) (f : Hom A B) :=
  f.hom

initialize_simps_projections Hom (hom' ‚Üí hom)

/-!
The results below duplicate the `ConcreteCategory` simp lemmas, but we can keep them for `dsimp`.
-/

@[simp]
lemma hom_id {A : AlgebraCat.{v} R} : (ùüô A : A ‚ü∂ A).hom = AlgHom.id R A := rfl

/- Provided for rewriting. -/
lemma id_apply (A : AlgebraCat.{v} R) (a : A) :
    (ùüô A : A ‚ü∂ A) a = a := by simp

@[simp]
lemma hom_comp {A B C : AlgebraCat.{v} R} (f : A ‚ü∂ B) (g : B ‚ü∂ C) :
    (f ‚â´ g).hom = g.hom.comp f.hom := rfl

/- Provided for rewriting. -/
lemma comp_apply {A B C : AlgebraCat.{v} R} (f : A ‚ü∂ B) (g : B ‚ü∂ C) (a : A) :
    (f ‚â´ g) a = g (f a) := by simp

@[ext]
lemma hom_ext {A B : AlgebraCat.{v} R} {f g : A ‚ü∂ B} (hf : f.hom = g.hom) : f = g :=
  Hom.ext hf

@[simp]
lemma hom_ofHom {R : Type u} [CommRing R] {X Y : Type v} [Ring X] [Algebra R X] [Ring Y]
    [Algebra R Y] (f : X ‚Üí‚Çê[R] Y) : (ofHom f).hom = f := rfl

@[simp]
lemma ofHom_hom {A B : AlgebraCat.{v} R} (f : A ‚ü∂ B) :
    ofHom (Hom.hom f) = f := rfl

@[simp]
lemma ofHom_id {X : Type v} [Ring X] [Algebra R X] : ofHom (AlgHom.id R X) = ùüô (of R X) := rfl

@[simp]
lemma ofHom_comp {X Y Z : Type v} [Ring X] [Ring Y] [Ring Z] [Algebra R X] [Algebra R Y]
    [Algebra R Z] (f : X ‚Üí‚Çê[R] Y) (g : Y ‚Üí‚Çê[R] Z) :
    ofHom (g.comp f) = ofHom f ‚â´ ofHom g :=
  rfl

lemma ofHom_apply {R : Type u} [CommRing R] {X Y : Type v} [Ring X] [Algebra R X] [Ring Y]
    [Algebra R Y] (f : X ‚Üí‚Çê[R] Y) (x : X) : ofHom f x = f x := rfl

lemma inv_hom_apply {A B : AlgebraCat.{v} R} (e : A ‚âÖ B) (x : A) : e.inv (e.hom x) = x := by
  rw [‚Üê comp_apply]
  simp

lemma hom_inv_apply {A B : AlgebraCat.{v} R} (e : A ‚âÖ B) (x : B) : e.hom (e.inv x) = x := by
  rw [‚Üê comp_apply]
  simp

instance : Inhabited (AlgebraCat R) :=
  ‚ü®of R R‚ü©

lemma forget_obj {A : AlgebraCat.{v} R} : (forget (AlgebraCat.{v} R)).obj A = A := rfl

lemma forget_map {A B : AlgebraCat.{v} R} (f : A ‚ü∂ B) :
    (forget (AlgebraCat.{v} R)).map f = f :=
  rfl

instance {S : AlgebraCat.{v} R} : Ring ((forget (AlgebraCat R)).obj S) :=
  (inferInstance : Ring S.carrier)

instance {S : AlgebraCat.{v} R} : Algebra R ((forget (AlgebraCat R)).obj S) :=
  (inferInstance : Algebra R S.carrier)

instance hasForgetToRing : HasForget‚ÇÇ (AlgebraCat.{v} R) RingCat.{v} where
  forget‚ÇÇ :=
    { obj := fun A => RingCat.of A
      map := fun f => RingCat.ofHom f.hom.toRingHom }

instance hasForgetToModule : HasForget‚ÇÇ (AlgebraCat.{v} R) (ModuleCat.{v} R) where
  forget‚ÇÇ :=
    { obj := fun M => ModuleCat.of R M
      map := fun f => ModuleCat.ofHom f.hom.toLinearMap }

@[simp]
lemma forget‚ÇÇ_module_obj (X : AlgebraCat.{v} R) :
    (forget‚ÇÇ (AlgebraCat.{v} R) (ModuleCat.{v} R)).obj X = ModuleCat.of R X :=
  rfl

@[simp]
lemma forget‚ÇÇ_module_map {X Y : AlgebraCat.{v} R} (f : X ‚ü∂ Y) :
    (forget‚ÇÇ (AlgebraCat.{v} R) (ModuleCat.{v} R)).map f = ModuleCat.ofHom f.hom.toLinearMap :=
  rfl

variable {R} in
/-- Forgetting to the underlying type and then building the bundled object returns the original
algebra. -/
@[simps]
def ofSelfIso (M : AlgebraCat.{v} R) : AlgebraCat.of R M ‚âÖ M where
  hom := ùüô M
  inv := ùüô M

/-- The "free algebra" functor, sending a type `S` to the free algebra on `S`. -/
@[simps! obj map]
def free : Type u ‚•§ AlgebraCat.{u} R where
  obj S := of R (FreeAlgebra R S)
  map f := ofHom <| FreeAlgebra.lift _ <| FreeAlgebra.Œπ _ ‚àò f

/-- The free/forget adjunction for `R`-algebras. -/
def adj : free.{u} R ‚ä£ forget (AlgebraCat.{u} R) :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun _ _ =>
        { toFun := fun f ‚Ü¶ (FreeAlgebra.lift _).symm f.hom
          invFun := fun f ‚Ü¶ ofHom <| (FreeAlgebra.lift _) f
          left_inv := fun f ‚Ü¶ by aesop
          right_inv := fun f ‚Ü¶ by simp [forget_obj, forget_map] } }

instance : (forget (AlgebraCat.{u} R)).IsRightAdjoint := (adj R).isRightAdjoint

end AlgebraCat

variable {R}
variable {X‚ÇÅ X‚ÇÇ : Type u}

/-- Build an isomorphism in the category `AlgebraCat R` from a `AlgEquiv` between `Algebra`s. -/
@[simps]
def AlgEquiv.toAlgebraIso {g‚ÇÅ : Ring X‚ÇÅ} {g‚ÇÇ : Ring X‚ÇÇ} {m‚ÇÅ : Algebra R X‚ÇÅ} {m‚ÇÇ : Algebra R X‚ÇÇ}
    (e : X‚ÇÅ ‚âÉ‚Çê[R] X‚ÇÇ) : AlgebraCat.of R X‚ÇÅ ‚âÖ AlgebraCat.of R X‚ÇÇ where
  hom := AlgebraCat.ofHom (e : X‚ÇÅ ‚Üí‚Çê[R] X‚ÇÇ)
  inv := AlgebraCat.ofHom (e.symm : X‚ÇÇ ‚Üí‚Çê[R] X‚ÇÅ)

namespace CategoryTheory.Iso

/-- Build a `AlgEquiv` from an isomorphism in the category `AlgebraCat R`. -/
@[simps]
def toAlgEquiv {X Y : AlgebraCat R} (i : X ‚âÖ Y) : X ‚âÉ‚Çê[R] Y :=
  { i.hom.hom with
    toFun := i.hom
    invFun := i.inv
    left_inv := fun x ‚Ü¶ by simp
    right_inv := fun x ‚Ü¶ by simp }

end CategoryTheory.Iso

/-- Algebra equivalences between `Algebra`s are the same as (isomorphic to) isomorphisms in
`AlgebraCat`. -/
@[simps]
def algEquivIsoAlgebraIso {X Y : Type u} [Ring X] [Ring Y] [Algebra R X] [Algebra R Y] :
    (X ‚âÉ‚Çê[R] Y) ‚âÖ AlgebraCat.of R X ‚âÖ AlgebraCat.of R Y where
  hom e := e.toAlgebraIso
  inv i := i.toAlgEquiv

instance AlgebraCat.forget_reflects_isos : (forget (AlgebraCat.{u} R)).ReflectsIsomorphisms where
  reflects {X Y} f _ := by
    let i := asIso ((forget (AlgebraCat.{u} R)).map f)
    let e : X ‚âÉ‚Çê[R] Y := { f.hom, i.toEquiv with }
    exact e.toAlgebraIso.isIso_hom
