/-
Copyright (c) 2023 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.CategoryTheory.Monoidal.Transport
import Mathlib.Algebra.Category.AlgCat.Basic
import Mathlib.Algebra.Category.ModuleCat.Monoidal.Basic
import Mathlib.RingTheory.TensorProduct.Maps

/-!
# The monoidal category structure on R-algebras
-/

open CategoryTheory
open scoped MonoidalCategory

universe v u

variable {R : Type u} [CommRing R]

namespace AlgCat

noncomputable section

namespace instMonoidalCategory

open scoped TensorProduct

/-- Auxiliary definition used to fight a timeout when building
`AlgCat.instMonoidalCategory`. -/
@[simps!]
noncomputable abbrev tensorObj (X Y : AlgCat.{u} R) : AlgCat.{u} R :=
  of R (X ‚äó[R] Y)

/-- Auxiliary definition used to fight a timeout when building
`AlgCat.instMonoidalCategory`. -/
noncomputable abbrev tensorHom {W X Y Z : AlgCat.{u} R} (f : W ‚ü∂ X) (g : Y ‚ü∂ Z) :
    tensorObj W Y ‚ü∂ tensorObj X Z :=
  ofHom <| Algebra.TensorProduct.map f.hom g.hom

open MonoidalCategory

end instMonoidalCategory

open instMonoidalCategory

instance : MonoidalCategoryStruct (AlgCat.{u} R) where
  tensorObj := instMonoidalCategory.tensorObj
  whiskerLeft X _ _ f := tensorHom (ùüô X) f
  whiskerRight {X‚ÇÅ X‚ÇÇ} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) Y := tensorHom f (ùüô Y)
  tensorHom := tensorHom
  tensorUnit := of R R
  associator X Y Z := (Algebra.TensorProduct.assoc R R X Y Z).toAlgebraIso
  leftUnitor X := (Algebra.TensorProduct.lid R X).toAlgebraIso
  rightUnitor X := (Algebra.TensorProduct.rid R R X).toAlgebraIso

theorem hom_tensorHom {K L M N : AlgCat.{u} R} (f : K ‚ü∂ L) (g : M ‚ü∂ N) :
    (f ‚äó‚Çò g).hom = Algebra.TensorProduct.map f.hom g.hom :=
  rfl

theorem hom_whiskerLeft (L : AlgCat.{u} R) {M N : AlgCat.{u} R} (f : M ‚ü∂ N) :
    (L ‚óÅ f).hom = Algebra.TensorProduct.map (.id _ _) f.hom :=
  rfl

theorem hom_whiskerRight {L M : AlgCat.{u} R} (f : L ‚ü∂ M) (N : AlgCat.{u} R) :
    (f ‚ñ∑ N).hom = Algebra.TensorProduct.map f.hom (.id _ _) :=
  rfl

theorem hom_hom_leftUnitor {M : AlgCat.{u} R} :
    (Œª_ M).hom.hom = (Algebra.TensorProduct.lid _ _).toAlgHom :=
  rfl

theorem hom_inv_leftUnitor {M : AlgCat.{u} R} :
    (Œª_ M).inv.hom = (Algebra.TensorProduct.lid _ _).symm.toAlgHom :=
  rfl

theorem hom_hom_rightUnitor {M : AlgCat.{u} R} :
    (œÅ_ M).hom.hom = (Algebra.TensorProduct.rid _ _ _).toAlgHom :=
  rfl

theorem hom_inv_rightUnitor {M : AlgCat.{u} R} :
    (œÅ_ M).inv.hom = (Algebra.TensorProduct.rid _ _ _).symm.toAlgHom :=
  rfl

theorem hom_hom_associator {M N K : AlgCat.{u} R} :
    (Œ±_ M N K).hom.hom = (Algebra.TensorProduct.assoc R R M N K).toAlgHom :=
  rfl

theorem hom_inv_associator {M N K : AlgCat.{u} R} :
    (Œ±_ M N K).inv.hom = (Algebra.TensorProduct.assoc R R M N K).symm.toAlgHom :=
  rfl

noncomputable instance instMonoidalCategory : MonoidalCategory (AlgCat.{u} R) :=
  Monoidal.induced
    (forget‚ÇÇ (AlgCat R) (ModuleCat R))
    { ŒºIso := fun _ _ => Iso.refl _
      ŒµIso := Iso.refl _
      associator_eq := fun _ _ _ =>
        ModuleCat.hom_ext <| TensorProduct.ext_threefold (fun _ _ _ => rfl)
      leftUnitor_eq := fun _ => ModuleCat.hom_ext <| TensorProduct.ext' (fun _ _ => rfl)
      rightUnitor_eq := fun _ => ModuleCat.hom_ext <| TensorProduct.ext' (fun _ _ => rfl) }

/-- `forget‚ÇÇ (AlgCat R) (ModuleCat R)` as a monoidal functor. -/
example : (forget‚ÇÇ (AlgCat R) (ModuleCat R)).Monoidal := inferInstance

end

end AlgCat
