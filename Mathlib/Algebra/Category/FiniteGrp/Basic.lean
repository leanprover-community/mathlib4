/-
Copyright (c) 2024 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Nailin Guan, Yuyang Zhao
-/
import Mathlib.Algebra.Category.Grp.Basic

/-!

## Main definitions and results

* `FiniteGrp` is the type of finite groups.

-/

universe u v

open CategoryTheory

/-- The structure of finite groups -/
@[pp_with_univ]
structure FiniteGrp where
  /--A group that is finite-/
  toGrp : Grp
  [isFinite : Finite toGrp]

namespace FiniteGrp

instance : CoeSort FiniteGrp.{u} (Type u) where
  coe G := G.toGrp

instance : Category FiniteGrp := InducedCategory.category FiniteGrp.toGrp

instance : ConcreteCategory FiniteGrp := InducedCategory.concreteCategory FiniteGrp.toGrp

instance (G : FiniteGrp) : Group G := inferInstanceAs <| Group G.toGrp

instance (G : FiniteGrp) : Finite G := G.isFinite

instance (G H : FiniteGrp) : FunLike (G ⟶ H) G H :=
  inferInstanceAs <| FunLike (G →* H) G H

instance (G H : FiniteGrp) : MonoidHomClass (G ⟶ H) G H :=
  inferInstanceAs <| MonoidHomClass (G →* H) G H

/--Making a finite group into a FiniteGrp-/
def of (G : Type u) [Group G] [Finite G] : FiniteGrp where
  toGrp := Grp.of G
  isFinite := ‹_›

/--The morphisms between FiniteGrp-/
def ofHom {X Y : Type u} [Group X] [Finite X] [Group Y] [Finite Y] (f : X →* Y) : of X ⟶ of Y :=
  Grp.ofHom f

lemma ofHom_apply {X Y : Type u} [Group X] [Finite X] [Group Y] [Finite Y] (f : X →* Y) (x : X) :
    ofHom f x = f x :=
  rfl

end FiniteGrp
