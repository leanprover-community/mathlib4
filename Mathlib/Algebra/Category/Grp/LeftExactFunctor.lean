/-
Copyright (c) 2025 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.Algebra.Category.Grp.CartesianMonoidal
import Mathlib.Algebra.Category.Grp.EquivalenceGroupAddGroup
import Mathlib.CategoryTheory.Monoidal.Internal.Types.CommGrp_
import Mathlib.CategoryTheory.Preadditive.AdditiveFunctor
import Mathlib.CategoryTheory.Preadditive.CommGrp_

/-!
# The forgetful functor `(C â¥¤â‚— AddCommGroup) â¥¤ (C â¥¤â‚— Type v)` is an equivalence

This is true as long as `C` is additive.

Here, `C â¥¤â‚— D` is the category of finite-limits-preserving functors from `C` to `D`.

To construct a functor from `C â¥¤â‚— Type v` to `C â¥¤â‚— AddCommGrpCat.{v}`, notice that a left-exact
functor `F : C â¥¤ Type v` induces a functor `CommGrp C â¥¤ CommGrp (Type v)`. But `CommGrp C` is
equivalent to `C`, and `CommGrp (Type v)` is equivalent to `AddCommGrpCat.{v}`, so we turn this
into a functor `C â¥¤ AddCommGrpCat.{v}`. By construction, composing with the forgetful
functor recovers the functor we started with, so since the forgetful functor reflects finite
limits and `F` preserves finite limits, our constructed functor also preserves finite limits. It
can be shown that this construction gives a quasi-inverse to the whiskering operation
`(C â¥¤â‚— AddCommGrpCat.{v}) â¥¤ (C â¥¤â‚— Type v)`.
-/

open CategoryTheory MonoidalCategory Limits


universe v v' u u'

namespace AddCommGrpCat

section

variable {C : Type u} [Category.{v} C] [Preadditive C] [HasFiniteBiproducts C]

namespace leftExactFunctorForgetEquivalence

attribute [local instance] hasFiniteProducts_of_hasFiniteBiproducts
attribute [local instance] AddCommGrpCat.cartesianMonoidalCategoryAddCommGrp

private noncomputable local instance : CartesianMonoidalCategory C := .ofHasFiniteProducts

private noncomputable local instance : BraidedCategory C := .ofCartesianMonoidalCategory

/-- Implementation, see `leftExactFunctorForgetEquivalence`. -/
noncomputable def inverseAux : (C â¥¤â‚— Type v) â¥¤ C â¥¤ AddCommGrpCat.{v} :=
  Functor.mapCommGrpFunctor â‹™
    (Functor.whiskeringLeft _ _ _).obj Preadditive.commGrpEquivalence.functor â‹™
      (Functor.whiskeringRight _ _ _).obj
        (commGrpTypeEquivalenceCommGrp.functor â‹™ commGroupAddCommGroupEquivalence.functor)

instance (F : C â¥¤â‚— Type v) : PreservesFiniteLimits (inverseAux.obj F) where
  preservesFiniteLimits J _ _ :=
    have : PreservesLimitsOfShape J (inverseAux.obj F â‹™ forget AddCommGrpCat) :=
      inferInstanceAs (PreservesLimitsOfShape J F.1)
    preservesLimitsOfShape_of_reflects_of_preserves _ (forget AddCommGrpCat)

/-- Implementation, see `leftExactFunctorForgetEquivalence`. -/
noncomputable def inverse : (C â¥¤â‚— Type v) â¥¤ (C â¥¤â‚— AddCommGrp.{v}) :=
  ObjectProperty.lift _ inverseAux (by simp only [leftExactFunctor_iff]; infer_instance)

open scoped MonObj

attribute [-instance] Functor.LaxMonoidal.comp Functor.Monoidal.instComp in
/-- Implementation, see `leftExactFunctorForgetEquivalence`.
This is the complicated bit, where we show that forgetting the group structure in the image of
`F` and then reconstructing it recovers the group structure we started with. -/
noncomputable def unitIsoAux (F : C â¥¤ AddCommGrpCat.{v}) [PreservesFiniteLimits F] (X : C) :
    letI : (F â‹™ forget AddCommGrpCat).Braided := .ofChosenFiniteProducts _
    commGrpTypeEquivalenceCommGrp.inverse.obj (AddCommGrpCat.toCommGrp.obj (F.obj X)) â‰…
      (F â‹™ forget AddCommGrpCat).mapCommGrp.obj (Preadditive.commGrpEquivalence.functor.obj X) := by
  letI : (F â‹™ forget AddCommGrpCat).Braided := .ofChosenFiniteProducts _
  letI : F.Monoidal := .ofChosenFiniteProducts _
  refine CommGrp.mkIso Multiplicative.toAdd.toIso (by
    erw [Functor.mapCommGrp_obj_grp_one]
    cat_disch) ?_
  dsimp [-Functor.comp_map, -ConcreteCategory.forget_map_eq_coe, -forget_map]
  have : F.Additive := Functor.additive_of_preserves_binary_products _
  simp only [Category.id_comp]
  erw [Functor.mapCommGrp_obj_grp_mul]
  erw [Functor.comp_map, F.map_add, Functor.Monoidal.Î¼_comp F (forget AddCommGrpCat) X X,
    Category.assoc, â† Functor.map_comp, Preadditive.comp_add, Functor.Monoidal.Î¼_fst,
    Functor.Monoidal.Î¼_snd]
  cat_disch

/-- Implementation, see `leftExactFunctorForgetEquivalence`. -/
noncomputable def unitIso : ðŸ­ (C â¥¤â‚— AddCommGrpCat) â‰…
    (LeftExactFunctor.whiskeringRight _ _ _).obj (LeftExactFunctor.of (forget _)) â‹™ inverse :=
  NatIso.ofComponents (fun F => InducedCategory.isoMk (NatIso.ofComponents (fun X =>
    commGroupAddCommGroupEquivalence.counitIso.app _ â‰ªâ‰«
      (CommGrpCat.toAddCommGrp.mapIso (commGrpTypeEquivalenceCommGrp.counitIso.app
        (AddCommGrpCat.toCommGrp.obj (F.obj.obj X)))).symm â‰ªâ‰«
      CommGrpCat.toAddCommGrp.mapIso
        (CommGrpTypeEquivalenceCommGrp.functor.mapIso (unitIsoAux F.obj X)))))

end leftExactFunctorForgetEquivalence

variable (C) in
/-- If `C` is an additive category, the forgetful functor `(C â¥¤â‚— AddCommGroup) â¥¤ (C â¥¤â‚— Type v)` is
an equivalence. -/
noncomputable def leftExactFunctorForgetEquivalence : (C â¥¤â‚— AddCommGrpCat.{v}) â‰Œ (C â¥¤â‚— Type v) where
  functor := (LeftExactFunctor.whiskeringRight _ _ _).obj (LeftExactFunctor.of (forget _))
  inverse := leftExactFunctorForgetEquivalence.inverse
  unitIso := leftExactFunctorForgetEquivalence.unitIso
  counitIso := Iso.refl _

end

end AddCommGrpCat
