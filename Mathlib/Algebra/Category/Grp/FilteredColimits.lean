/-
Copyright (c) 2021 Justus Springer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Justus Springer
-/
import Mathlib.Algebra.Category.Grp.Basic
import Mathlib.Algebra.Category.MonCat.FilteredColimits

/-!
# The forgetful functor from (commutative) (additive) groups preserves filtered colimits.

Forgetful functors from algebraic categories usually don't preserve colimits. However, they tend
to preserve _filtered_ colimits.

In this file, we start with a small filtered category `J` and a functor `F : J ‚•§ Grp`.
We show that the colimit of `F ‚ãô forget‚ÇÇ Grp MonCat` (in `MonCat`) carries the structure of a
group,
thereby showing that the forgetful functor `forget‚ÇÇ Grp MonCat` preserves filtered colimits.
In particular, this implies that `forget Grp` preserves filtered colimits.
Similarly for `AddGrp`, `CommGrp` and `AddCommGrp`.

-/


universe v u

noncomputable section

open CategoryTheory Limits

open IsFiltered renaming max ‚Üí max' -- avoid name collision with `_root_.max`.

namespace Grp.FilteredColimits

section

open MonCat.FilteredColimits (colimit_one_eq colimit_mul_mk_eq)

-- Mathlib3 used parameters here, mainly so we could have the abbreviations `G` and `G.mk` below,
-- without passing around `F` all the time.
variable {J : Type v} [SmallCategory J] [IsFiltered J] (F : J ‚•§ Grp.{max v u})

/-- The colimit of `F ‚ãô forget‚ÇÇ Grp MonCat` in the category `MonCat`.
In the following, we will show that this has the structure of a group.
-/
@[to_additive
  "The colimit of `F ‚ãô forget‚ÇÇ AddGrp AddMonCat` in the category `AddMonCat`.
  In the following, we will show that this has the structure of an additive group."]
noncomputable abbrev G : MonCat :=
  MonCat.FilteredColimits.colimit.{v, u} (F ‚ãô forget‚ÇÇ Grp MonCat.{max v u})

/-- The canonical projection into the colimit, as a quotient type. -/
@[to_additive "The canonical projection into the colimit, as a quotient type."]
abbrev G.mk : (Œ£ j, F.obj j) ‚Üí G.{v, u} F :=
  Quot.mk (Types.Quot.Rel (F ‚ãô forget Grp.{max v u}))

@[to_additive]
theorem G.mk_eq (x y : Œ£ j, F.obj j)
    (h : ‚àÉ (k : J) (f : x.1 ‚ü∂ k) (g : y.1 ‚ü∂ k), F.map f x.2 = F.map g y.2) :
    G.mk.{v, u} F x = G.mk F y :=
  Quot.eqvGen_sound (Types.FilteredColimit.eqvGen_quot_rel_of_rel (F ‚ãô forget Grp) x y h)

/-- The "unlifted" version of taking inverses in the colimit. -/
@[to_additive "The \"unlifted\" version of negation in the colimit."]
def colimitInvAux (x : Œ£ j, F.obj j) : G.{v, u} F :=
  G.mk F ‚ü®x.1, x.2‚Åª¬π‚ü©

@[to_additive]
theorem colimitInvAux_eq_of_rel (x y : Œ£ j, F.obj j)
    (h : Types.FilteredColimit.Rel (F ‚ãô forget Grp) x y) :
    colimitInvAux.{v, u} F x = colimitInvAux F y := by
  apply G.mk_eq
  obtain ‚ü®k, f, g, hfg‚ü© := h
  use k, f, g
  rw [MonoidHom.map_inv, MonoidHom.map_inv, inv_inj]
  exact hfg

/-- Taking inverses in the colimit. See also `colimitInvAux`. -/
@[to_additive "Negation in the colimit. See also `colimitNegAux`."]
instance colimitInv : Inv (G.{v, u} F) where
  inv x := by
    refine Quot.lift (colimitInvAux.{v, u} F) ?_ x
    intro x y h
    apply colimitInvAux_eq_of_rel
    apply Types.FilteredColimit.rel_of_quot_rel
    exact h

@[to_additive (attr := simp)]
theorem colimit_inv_mk_eq (x : Œ£ j, F.obj j) : (G.mk.{v, u} F x)‚Åª¬π = G.mk F ‚ü®x.1, x.2‚Åª¬π‚ü© :=
  rfl

@[to_additive]
noncomputable instance colimitGroup : Group (G.{v, u} F) :=
  { colimitInv.{v, u} F, (G.{v, u} F).str with
    inv_mul_cancel := fun x => by
      refine Quot.inductionOn x ?_; clear x; intro x
      obtain ‚ü®j, x‚ü© := x
      erw [colimit_inv_mk_eq]
      erw [colimit_mul_mk_eq (F ‚ãô forget‚ÇÇ Grp MonCat.{max v u}) ‚ü®j, _‚ü© ‚ü®j, _‚ü© j (ùüô j) (ùüô j)]
      simp [colimit_one_eq (F ‚ãô forget‚ÇÇ Grp MonCat.{max v u}) j] }

/-- The bundled group giving the filtered colimit of a diagram. -/
@[to_additive "The bundled additive group giving the filtered colimit of a diagram."]
noncomputable def colimit : Grp.{max v u} :=
  Grp.of (G.{v, u} F)

/-- The cocone over the proposed colimit group. -/
@[to_additive "The cocone over the proposed colimit additive group."]
noncomputable def colimitCocone : Cocone F where
  pt := colimit.{v, u} F
  Œπ.app J := Grp.ofHom ((MonCat.FilteredColimits.colimitCocone
    (F ‚ãô forget‚ÇÇ Grp MonCat)).Œπ.app J).hom
  Œπ.naturality _ _ f := (forget‚ÇÇ _ MonCat).map_injective
    ((MonCat.FilteredColimits.colimitCocone _).Œπ.naturality f)

/-- The proposed colimit cocone is a colimit in `Grp`. -/
@[to_additive "The proposed colimit cocone is a colimit in `AddGroup`."]
noncomputable def colimitCoconeIsColimit : IsColimit (colimitCocone.{v, u} F) where
  desc t := Grp.ofHom
    (MonCat.FilteredColimits.colimitDesc.{v, u} (F ‚ãô forget‚ÇÇ Grp MonCat.{max v u})
      ((forget‚ÇÇ Grp MonCat).mapCocone t)).hom
  fac t j :=
    ConcreteCategory.coe_ext <|
      (Types.TypeMax.colimitCoconeIsColimit.{v, u} (F ‚ãô forget Grp)).fac
      ((forget Grp).mapCocone t) j
  uniq t _ h :=
    ConcreteCategory.coe_ext <|
      (Types.TypeMax.colimitCoconeIsColimit.{v, u} (F ‚ãô forget Grp)).uniq
      ((forget Grp).mapCocone t) _
        fun j => funext fun x => ConcreteCategory.congr_hom (h j) x

@[to_additive forget‚ÇÇAddMon_preservesFilteredColimits]
noncomputable instance forget‚ÇÇMon_preservesFilteredColimits :
    PreservesFilteredColimits.{u} (forget‚ÇÇ Grp.{u} MonCat.{u}) where
      preserves_filtered_colimits x hx1 _ :=
      letI : Category.{u, u} x := hx1
      ‚ü®fun {F} => preservesColimit_of_preserves_colimit_cocone (colimitCoconeIsColimit.{u, u} F)
          (MonCat.FilteredColimits.colimitCoconeIsColimit.{u, u} _)‚ü©

@[to_additive]
noncomputable instance forget_preservesFilteredColimits :
    PreservesFilteredColimits (forget Grp.{u}) :=
  Limits.comp_preservesFilteredColimits (forget‚ÇÇ Grp MonCat) (forget MonCat.{u})

end

end Grp.FilteredColimits

namespace CommGrp.FilteredColimits

section

-- We use parameters here, mainly so we can have the abbreviation `G` below, without
-- passing around `F` all the time.
variable {J : Type v} [SmallCategory J] [IsFiltered J] (F : J ‚•§ CommGrp.{max v u})

/-- The colimit of `F ‚ãô forget‚ÇÇ CommGrp Grp` in the category `Grp`.
In the following, we will show that this has the structure of a _commutative_ group.
-/
@[to_additive
  "The colimit of `F ‚ãô forget‚ÇÇ AddCommGrp AddGrp` in the category `AddGrp`.
  In the following, we will show that this has the structure of a _commutative_ additive group."]
noncomputable abbrev G : Grp.{max v u} :=
  Grp.FilteredColimits.colimit.{v, u} (F ‚ãô forget‚ÇÇ CommGrp.{max v u} Grp.{max v u})

@[to_additive]
noncomputable instance colimitCommGroup : CommGroup.{max v u} (G.{v, u} F) :=
  { (G F).str,
    CommMonCat.FilteredColimits.colimitCommMonoid
      (F ‚ãô forget‚ÇÇ CommGrp CommMonCat.{max v u}) with }

/-- The bundled commutative group giving the filtered colimit of a diagram. -/
@[to_additive "The bundled additive commutative group giving the filtered colimit of a diagram."]
noncomputable def colimit : CommGrp :=
  CommGrp.of (G.{v, u} F)

/-- The cocone over the proposed colimit commutative group. -/
@[to_additive "The cocone over the proposed colimit additive commutative group."]
noncomputable def colimitCocone : Cocone F where
  pt := colimit.{v, u} F
  Œπ.app J := CommGrp.ofHom
    ((Grp.FilteredColimits.colimitCocone (F ‚ãô forget‚ÇÇ CommGrp Grp)).Œπ.app J).hom
  Œπ.naturality _ _ f := (forget‚ÇÇ _ Grp).map_injective
    ((Grp.FilteredColimits.colimitCocone _).Œπ.naturality f)

/-- The proposed colimit cocone is a colimit in `CommGrp`. -/
@[to_additive "The proposed colimit cocone is a colimit in `AddCommGroup`."]
noncomputable def colimitCoconeIsColimit : IsColimit (colimitCocone.{v, u} F) where
  desc t := CommGrp.ofHom
    ((Grp.FilteredColimits.colimitCoconeIsColimit.{v, u}
          (F ‚ãô forget‚ÇÇ CommGrp Grp.{max v u})).desc
      ((forget‚ÇÇ CommGrp Grp.{max v u}).mapCocone t)).hom
  fac t j :=
    ConcreteCategory.coe_ext <|
      (Types.TypeMax.colimitCoconeIsColimit.{v, u} (F ‚ãô forget CommGrp)).fac
        ((forget CommGrp).mapCocone t) j
  uniq t _ h :=
    ConcreteCategory.coe_ext <|
      (Types.TypeMax.colimitCoconeIsColimit.{v, u} (F ‚ãô forget CommGrp)).uniq
        ((forget CommGrp).mapCocone t) _ fun j => funext fun x => ConcreteCategory.congr_hom (h j) x

@[to_additive]
noncomputable instance forget‚ÇÇGroup_preservesFilteredColimits :
    PreservesFilteredColimits (forget‚ÇÇ CommGrp Grp.{u}) where
  preserves_filtered_colimits J hJ1 _ :=
    letI : Category J := hJ1
    { preservesColimit := fun {F} =>
        preservesColimit_of_preserves_colimit_cocone (colimitCoconeIsColimit.{u, u} F)
          (Grp.FilteredColimits.colimitCoconeIsColimit.{u, u}
            (F ‚ãô forget‚ÇÇ CommGrp Grp.{u})) }

@[to_additive]
noncomputable instance forget_preservesFilteredColimits :
    PreservesFilteredColimits (forget CommGrp.{u}) :=
  Limits.comp_preservesFilteredColimits (forget‚ÇÇ CommGrp Grp) (forget Grp.{u})

end

end CommGrp.FilteredColimits
