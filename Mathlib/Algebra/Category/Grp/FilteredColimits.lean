/-
Copyright (c) 2021 Justus Springer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Justus Springer
-/
import Mathlib.Algebra.Category.Grp.Basic
import Mathlib.Algebra.Category.MonCat.FilteredColimits

/-!
# The forgetful functor from (commutative) (additive) groups preserves filtered colimits.

Forgetful functors from algebraic categories usually don't preserve colimits. However, they tend
to preserve _filtered_ colimits.

In this file, we start with a small filtered category `J` and a functor `F : J â¥¤ Grp`.
We show that the colimit of `F â‹™ forgetâ‚‚ Grp MonCat` (in `MonCat`) carries the structure of a
group,
thereby showing that the forgetful functor `forgetâ‚‚ Grp MonCat` preserves filtered colimits.
In particular, this implies that `forget Grp` preserves filtered colimits.
Similarly for `AddGrp`, `CommGrp` and `AddCommGrp`.

-/


universe v u

noncomputable section

open CategoryTheory Limits

open IsFiltered renaming max â†’ max' -- avoid name collision with `_root_.max`.

namespace Grp.FilteredColimits

section

-- Mathlib3 used parameters here, mainly so we could have the abbreviations `G` and `G.mk` below,
-- without passing around `F` all the time.
variable {J : Type v} [SmallCategory J] [IsFiltered J] (F : J â¥¤ Grp.{max v u})

/-- The colimit of `F â‹™ forgetâ‚‚ Grp MonCat` in the category `MonCat`.
In the following, we will show that this has the structure of a group.
-/
@[to_additive
  /-- The colimit of `F â‹™ forgetâ‚‚ AddGrp AddMonCat` in the category `AddMonCat`.
  In the following, we will show that this has the structure of an additive group. -/]
noncomputable abbrev G : MonCat :=
  MonCat.FilteredColimits.colimit.{v, u} (F â‹™ forgetâ‚‚ Grp MonCat.{max v u})

/-- The canonical projection into the colimit, as a quotient type. -/
@[to_additive /-- The canonical projection into the colimit, as a quotient type. -/]
abbrev G.mk : (Î£ j, F.obj j) â†’ G.{v, u} F :=
  fun x â†¦ (F â‹™ forget Grp).Î¹ColimitType x.1 x.2

@[to_additive]
theorem G.mk_eq (x y : Î£ j, F.obj j)
    (h : âˆƒ (k : J) (f : x.1 âŸ¶ k) (g : y.1 âŸ¶ k), F.map f x.2 = F.map g y.2) :
    G.mk.{v, u} F x = G.mk F y :=
  Quot.eqvGen_sound (Types.FilteredColimit.eqvGen_colimitTypeRel_of_rel (F â‹™ forget Grp) x y h)

@[to_additive]
theorem colimit_one_eq (j : J) : (1 : G.{v, u} F) = G.mk F âŸ¨j, 1âŸ© :=
  MonCat.FilteredColimits.colimit_one_eq _ _

@[to_additive]
theorem colimit_mul_mk_eq (x y : Î£ j, F.obj j) (k : J) (f : x.1 âŸ¶ k) (g : y.1 âŸ¶ k) :
    G.mk.{v, u} F x * G.mk F y = G.mk F âŸ¨k, F.map f x.2 * F.map g y.2âŸ© :=
  MonCat.FilteredColimits.colimit_mul_mk_eq _ _ _ _ _ _

@[to_additive]
lemma colimit_mul_mk_eq' {j : J} (x y : F.obj j) :
    G.mk.{v, u} F âŸ¨j, xâŸ© * G.mk.{v, u} F âŸ¨j, yâŸ© = G.mk.{v, u} F âŸ¨j, x * yâŸ© := by
  simpa using colimit_mul_mk_eq F âŸ¨j, xâŸ© âŸ¨j, yâŸ© j (ðŸ™ _) (ðŸ™ _)

/-- The "unlifted" version of taking inverses in the colimit. -/
@[to_additive /-- The "unlifted" version of negation in the colimit. -/]
def colimitInvAux (x : Î£ j, F.obj j) : G.{v, u} F :=
  G.mk F âŸ¨x.1, x.2â»Â¹âŸ©

@[to_additive]
theorem colimitInvAux_eq_of_rel (x y : Î£ j, F.obj j)
    (h : Types.FilteredColimit.Rel (F â‹™ forget Grp) x y) :
    colimitInvAux.{v, u} F x = colimitInvAux F y := by
  apply G.mk_eq
  obtain âŸ¨k, f, g, hfgâŸ© := h
  use k, f, g
  rw [MonoidHom.map_inv, MonoidHom.map_inv, inv_inj]
  exact hfg

/-- Taking inverses in the colimit. See also `colimitInvAux`. -/
@[to_additive /-- Negation in the colimit. See also `colimitNegAux`. -/]
instance colimitInv : Inv (G.{v, u} F) where
  inv x := by
    refine Quot.lift (colimitInvAux.{v, u} F) ?_ x
    intro x y h
    apply colimitInvAux_eq_of_rel
    apply Types.FilteredColimit.rel_of_colimitTypeRel
    exact h

@[to_additive (attr := simp)]
theorem colimit_inv_mk_eq (x : Î£ j, F.obj j) : (G.mk.{v, u} F x)â»Â¹ = G.mk F âŸ¨x.1, x.2â»Â¹âŸ© :=
  rfl

@[to_additive]
noncomputable instance colimitGroup : Group (G.{v, u} F) :=
  { colimitInv.{v, u} F, (G.{v, u} F).str with
    inv_mul_cancel := fun x => by
      refine Quot.inductionOn x ?_; clear x; intro x
      change (G.mk _ _)â»Â¹ * G.mk _ _ = _
      obtain âŸ¨j, xâŸ© := x
      simp [colimit_inv_mk_eq, colimit_mul_mk_eq F âŸ¨j, _âŸ© âŸ¨j, _âŸ© j (ðŸ™ j) (ðŸ™ j),
        colimit_one_eq F j] }

/-- The bundled group giving the filtered colimit of a diagram. -/
@[to_additive /-- The bundled additive group giving the filtered colimit of a diagram. -/]
noncomputable def colimit : Grp.{max v u} :=
  Grp.of (G.{v, u} F)

/-- The cocone over the proposed colimit group. -/
@[to_additive /-- The cocone over the proposed colimit additive group. -/]
noncomputable def colimitCocone : Cocone F where
  pt := colimit.{v, u} F
  Î¹.app J := Grp.ofHom ((MonCat.FilteredColimits.colimitCocone
    (F â‹™ forgetâ‚‚ Grp MonCat)).Î¹.app J).hom
  Î¹.naturality _ _ f := (forgetâ‚‚ _ MonCat).map_injective
    ((MonCat.FilteredColimits.colimitCocone _).Î¹.naturality f)

/-- The proposed colimit cocone is a colimit in `Grp`. -/
@[to_additive /-- The proposed colimit cocone is a colimit in `AddGroup`. -/]
noncomputable def colimitCoconeIsColimit : IsColimit (colimitCocone.{v, u} F) :=
  isColimitOfReflects (forgetâ‚‚ _ MonCat)
    (MonCat.FilteredColimits.colimitCoconeIsColimit (F â‹™ forgetâ‚‚ Grp MonCat))

@[to_additive forgetâ‚‚AddMon_preservesFilteredColimits]
noncomputable instance forgetâ‚‚Mon_preservesFilteredColimits :
    PreservesFilteredColimits.{u} (forgetâ‚‚ Grp.{u} MonCat.{u}) where
      preserves_filtered_colimits x hx1 _ :=
      letI : Category.{u, u} x := hx1
      âŸ¨fun {F} => preservesColimit_of_preserves_colimit_cocone (colimitCoconeIsColimit.{u, u} F)
          (MonCat.FilteredColimits.colimitCoconeIsColimit.{u, u} _)âŸ©

@[to_additive]
noncomputable instance forget_preservesFilteredColimits :
    PreservesFilteredColimits (forget Grp.{u}) :=
  Limits.comp_preservesFilteredColimits (forgetâ‚‚ Grp MonCat) (forget MonCat.{u})

end

end Grp.FilteredColimits

namespace CommGrp.FilteredColimits

section

-- We use parameters here, mainly so we can have the abbreviation `G` below, without
-- passing around `F` all the time.
variable {J : Type v} [SmallCategory J] [IsFiltered J] (F : J â¥¤ CommGrp.{max v u})

/-- The colimit of `F â‹™ forgetâ‚‚ CommGrp Grp` in the category `Grp`.
In the following, we will show that this has the structure of a _commutative_ group.
-/
@[to_additive
  /-- The colimit of `F â‹™ forgetâ‚‚ AddCommGrp AddGrp` in the category `AddGrp`.
  In the following, we will show that this has the structure of a _commutative_ additive group. -/]
noncomputable abbrev G : Grp.{max v u} :=
  Grp.FilteredColimits.colimit.{v, u} (F â‹™ forgetâ‚‚ CommGrp.{max v u} Grp.{max v u})

@[to_additive]
noncomputable instance colimitCommGroup : CommGroup.{max v u} (G.{v, u} F) :=
  { (G F).str,
    CommMonCat.FilteredColimits.colimitCommMonoid
      (F â‹™ forgetâ‚‚ CommGrp CommMonCat.{max v u}) with }

/-- The bundled commutative group giving the filtered colimit of a diagram. -/
@[to_additive
/-- The bundled additive commutative group giving the filtered colimit of a diagram. -/]
noncomputable def colimit : CommGrp :=
  CommGrp.of (G.{v, u} F)

/-- The cocone over the proposed colimit commutative group. -/
@[to_additive /-- The cocone over the proposed colimit additive commutative group. -/]
noncomputable def colimitCocone : Cocone F where
  pt := colimit.{v, u} F
  Î¹.app J := CommGrp.ofHom
    ((Grp.FilteredColimits.colimitCocone (F â‹™ forgetâ‚‚ CommGrp Grp)).Î¹.app J).hom
  Î¹.naturality _ _ f := (forgetâ‚‚ _ Grp).map_injective
    ((Grp.FilteredColimits.colimitCocone _).Î¹.naturality f)

/-- The proposed colimit cocone is a colimit in `CommGrp`. -/
@[to_additive /-- The proposed colimit cocone is a colimit in `AddCommGroup`. -/]
noncomputable def colimitCoconeIsColimit : IsColimit (colimitCocone.{v, u} F) :=
  isColimitOfReflects (forgetâ‚‚ _ Grp)
    (Grp.FilteredColimits.colimitCoconeIsColimit (F â‹™ forgetâ‚‚ CommGrp Grp))

@[to_additive]
noncomputable instance forgetâ‚‚Group_preservesFilteredColimits :
    PreservesFilteredColimits (forgetâ‚‚ CommGrp Grp.{u}) where
  preserves_filtered_colimits J hJ1 _ :=
    letI : Category J := hJ1
    { preservesColimit := fun {F} =>
        preservesColimit_of_preserves_colimit_cocone (colimitCoconeIsColimit.{u, u} F)
          (Grp.FilteredColimits.colimitCoconeIsColimit.{u, u}
            (F â‹™ forgetâ‚‚ CommGrp Grp.{u})) }

@[to_additive]
noncomputable instance forget_preservesFilteredColimits :
    PreservesFilteredColimits (forget CommGrp.{u}) :=
  Limits.comp_preservesFilteredColimits (forgetâ‚‚ CommGrp Grp) (forget Grp.{u})

end

end CommGrp.FilteredColimits
