/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Algebra.Category.ModuleCat.Basic

/-!
The forgetful functor from ‚Ñ§-modules to additive commutative groups is
an equivalence of categories.

TODO:
either use this equivalence to transport the monoidal structure from `Module ‚Ñ§` to `Ab`,
or, having constructed that monoidal structure directly, show this functor is monoidal.
-/


open CategoryTheory

open CategoryTheory.Equivalence

universe u

namespace ModuleCat

/-- The forgetful functor from `‚Ñ§` modules to `AddCommGrp` is full. -/
instance forget‚ÇÇ_addCommGroup_full : (forget‚ÇÇ (ModuleCat ‚Ñ§) AddCommGrp.{u}).Full where
  map_surjective {A B}
    -- `AddMonoidHom.toIntLinearMap` doesn't work here because `A` and `B` are not
    -- definitionally equal to the canonical `AddCommGroup.toIntModule` module
    -- instances it expects.
    f := ‚ü®@LinearMap.mk _ _ _ _ _ _ _ _ _ A.isModule B.isModule
        { toFun := f,
          map_add' := AddMonoidHom.map_add (show A.carrier ‚Üí+ B.carrier from f) }
        (fun n x => by
          convert AddMonoidHom.map_zsmul (show A.carrier ‚Üí+ B.carrier from f) x n <;>
            ext <;> apply int_smul_eq_zsmul), rfl‚ü©

/-- The forgetful functor from `‚Ñ§` modules to `AddCommGrp` is essentially surjective. -/
instance forget‚ÇÇ_addCommGrp_essSurj : (forget‚ÇÇ (ModuleCat ‚Ñ§) AddCommGrp.{u}).EssSurj where
  mem_essImage A :=
    ‚ü®ModuleCat.of ‚Ñ§ A,
      ‚ü®{  hom := ùüô A
          inv := ùüô A }‚ü©‚ü©

noncomputable instance forget‚ÇÇAddCommGroupIsEquivalence :
    (forget‚ÇÇ (ModuleCat ‚Ñ§) AddCommGrp.{u}).IsEquivalence where

end ModuleCat
