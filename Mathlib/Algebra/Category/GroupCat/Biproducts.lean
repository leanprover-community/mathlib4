/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Algebra.Group.Pi
import Mathlib.Algebra.Category.GroupCat.Preadditive
import Mathlib.CategoryTheory.Preadditive.Biproducts
import Mathlib.Algebra.Category.GroupCat.Limits

#align_import algebra.category.Group.biproducts from "leanprover-community/mathlib"@"234ddfeaa5572bc13716dd215c6444410a679a8e"

/-!
# The category of abelian groups has finite biproducts
-/


open CategoryTheory

open CategoryTheory.Limits

open BigOperators

universe w u

namespace AddCommGroupCat
set_option linter.uppercaseLean3 false -- `AddCommGroup`

-- As `AddCommGroupCat` is preadditive, and has all limits, it automatically has biproducts.
instance : HasBinaryBiproducts AddCommGroupCat :=
  HasBinaryBiproducts.of_hasBinaryProducts

instance : HasFiniteBiproducts AddCommGroupCat :=
  HasFiniteBiproducts.of_hasFiniteProducts

-- We now construct explicit limit data,
-- so we can compare the biproducts to the usual unbundled constructions.
/-- Construct limit data for a binary product in `AddCommGroupCat`, using
`AddCommGroupCat.of (G √ó H)`.
-/
@[simps cone_pt isLimit_lift]
def binaryProductLimitCone (G H : AddCommGroupCat.{u}) : Limits.LimitCone (pair G H) where
  cone :=
    { pt := AddCommGroupCat.of (G √ó H)
      œÄ :=
        { app := fun j =>
            Discrete.casesOn j fun j =>
              WalkingPair.casesOn j (AddMonoidHom.fst G H) (AddMonoidHom.snd G H)
          naturality := by rintro ‚ü®‚ü®‚ü©‚ü© ‚ü®‚ü®‚ü©‚ü© ‚ü®‚ü®‚ü®‚ü©‚ü©‚ü© <;> rfl } }
                           -- ‚ä¢ ((Functor.const (Discrete WalkingPair)).obj (of (‚ÜëG √ó ‚ÜëH))).map { down := {  ‚Ä¶
                                                       -- üéâ no goals
                                                       -- üéâ no goals
  isLimit :=
    { lift := fun s => AddMonoidHom.prod (s.œÄ.app ‚ü®WalkingPair.left‚ü©) (s.œÄ.app ‚ü®WalkingPair.right‚ü©)
      fac := by rintro s (‚ü®‚ü© | ‚ü®‚ü©) <;> rfl
                -- ‚ä¢ (fun s => AddMonoidHom.prod (NatTrans.app s.œÄ { as := WalkingPair.left }) (N ‚Ä¶
                                       -- üéâ no goals
                                       -- üéâ no goals
      uniq := fun s m w => by
        simp_rw [‚Üê w ‚ü®WalkingPair.left‚ü©, ‚Üê w ‚ü®WalkingPair.right‚ü©]
        -- ‚ä¢ m = AddMonoidHom.prod (m ‚â´ AddMonoidHom.fst ‚ÜëG ‚ÜëH) (m ‚â´ AddMonoidHom.snd ‚ÜëG  ‚Ä¶
        rfl }
        -- üéâ no goals
#align AddCommGroup.binary_product_limit_cone AddCommGroupCat.binaryProductLimitCone

@[simp]
theorem binaryProductLimitCone_cone_œÄ_app_left (G H : AddCommGroupCat.{u}) :
    (binaryProductLimitCone G H).cone.œÄ.app ‚ü®WalkingPair.left‚ü© = AddMonoidHom.fst G H :=
  rfl
#align AddCommGroup.binary_product_limit_cone_cone_œÄ_app_left AddCommGroupCat.binaryProductLimitCone_cone_œÄ_app_left

@[simp]
theorem binaryProductLimitCone_cone_œÄ_app_right (G H : AddCommGroupCat.{u}) :
    (binaryProductLimitCone G H).cone.œÄ.app ‚ü®WalkingPair.right‚ü© = AddMonoidHom.snd G H :=
  rfl
#align AddCommGroup.binary_product_limit_cone_cone_œÄ_app_right AddCommGroupCat.binaryProductLimitCone_cone_œÄ_app_right

/-- We verify that the biproduct in `AddCommGroupCat` is isomorphic to
the cartesian product of the underlying types:
-/
@[simps! hom_apply]
noncomputable def biprodIsoProd (G H : AddCommGroupCat.{u}) :
    (G ‚äû H : AddCommGroupCat) ‚âÖ AddCommGroupCat.of (G √ó H) :=
  IsLimit.conePointUniqueUpToIso (BinaryBiproduct.isLimit G H) (binaryProductLimitCone G H).isLimit
#align AddCommGroup.biprod_iso_prod AddCommGroupCat.biprodIsoProd

@[simp, elementwise]
theorem biprodIsoProd_inv_comp_fst (G H : AddCommGroupCat.{u}) :
    (biprodIsoProd G H).inv ‚â´ biprod.fst = AddMonoidHom.fst G H :=
  IsLimit.conePointUniqueUpToIso_inv_comp _ _ (Discrete.mk WalkingPair.left)
#align AddCommGroup.biprod_iso_prod_inv_comp_fst AddCommGroupCat.biprodIsoProd_inv_comp_fst

@[simp, elementwise]
theorem biprodIsoProd_inv_comp_snd (G H : AddCommGroupCat.{u}) :
    (biprodIsoProd G H).inv ‚â´ biprod.snd = AddMonoidHom.snd G H :=
  IsLimit.conePointUniqueUpToIso_inv_comp _ _ (Discrete.mk WalkingPair.right)
#align AddCommGroup.biprod_iso_prod_inv_comp_snd AddCommGroupCat.biprodIsoProd_inv_comp_snd

namespace HasLimit

variable {J : Type w} (f : J ‚Üí AddCommGroupCat.{max w u})

/-- The map from an arbitrary cone over an indexed family of abelian groups
to the cartesian product of those groups.
-/
@[simps]
def lift (s : Fan f) : s.pt ‚ü∂ AddCommGroupCat.of (‚àÄ j, f j) where
  toFun x j := s.œÄ.app ‚ü®j‚ü© x
  map_zero' := by
    simp only [Functor.const_obj_obj, map_zero]
    -- ‚ä¢ (fun j => 0) = 0
    rfl
    -- üéâ no goals
  map_add' x y := by
    simp only [Functor.const_obj_obj, map_add]
    -- ‚ä¢ (fun j => ‚Üë(NatTrans.app s.œÄ { as := j }) x + ‚Üë(NatTrans.app s.œÄ { as := j } ‚Ä¶
    rfl
    -- üéâ no goals
#align AddCommGroup.has_limit.lift AddCommGroupCat.HasLimit.lift

/-- Construct limit data for a product in `AddCommGroupCat`, using
`AddCommGroupCat.of (‚àÄ j, F.obj j)`.
-/
@[simps]
def productLimitCone : Limits.LimitCone (Discrete.functor f) where
  cone :=
    { pt := AddCommGroupCat.of (‚àÄ j, f j)
      œÄ := Discrete.natTrans fun j => Pi.evalAddMonoidHom (fun j => f j) j.as }
  isLimit :=
    { lift := lift.{_, u} f
      fac := fun s j => rfl
      uniq := fun s m w => by
        ext x
        -- ‚ä¢ ‚Üëm x = ‚Üë(lift f s) x
        funext j
        -- ‚ä¢ ‚Üëm x j = ‚Üë(lift f s) x j
        exact congr_arg (fun g : s.pt ‚ü∂ f j => (g : s.pt ‚Üí f j) x) (w ‚ü®j‚ü©) }
        -- üéâ no goals
#align AddCommGroup.has_limit.product_limit_cone AddCommGroupCat.HasLimit.productLimitCone

end HasLimit

open HasLimit

variable {J : Type} [Fintype J]

/-- We verify that the biproduct we've just defined is isomorphic to the `AddCommGroupCat` structure
on the dependent function type.
-/
@[simps! hom_apply]
noncomputable def biproductIsoPi (f : J ‚Üí AddCommGroupCat.{u}) :
    (‚®Å f : AddCommGroupCat) ‚âÖ AddCommGroupCat.of (‚àÄ j, f j) :=
  IsLimit.conePointUniqueUpToIso (biproduct.isLimit f) (productLimitCone f).isLimit
#align AddCommGroup.biproduct_iso_pi AddCommGroupCat.biproductIsoPi

@[simp, elementwise]
theorem biproductIsoPi_inv_comp_œÄ (f : J ‚Üí AddCommGroupCat.{u}) (j : J) :
    (biproductIsoPi f).inv ‚â´ biproduct.œÄ f j = Pi.evalAddMonoidHom (fun j => f j) j :=
  IsLimit.conePointUniqueUpToIso_inv_comp _ _ (Discrete.mk j)
#align AddCommGroup.biproduct_iso_pi_inv_comp_œÄ AddCommGroupCat.biproductIsoPi_inv_comp_œÄ

end AddCommGroupCat
