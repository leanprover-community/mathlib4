/-
Copyright (c) 2023 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies
-/
import Mathlib.Data.Int.ModEq
import Mathlib.GroupTheory.QuotientGroup

#align_import algebra.modeq from "leanprover-community/mathlib"@"a07d750983b94c530ab69a726862c2ab6802b38c"

/-!
# Equality modulo an element

This file defines equality modulo an element in a commutative group.

## Main definitions

* `a ‚â° b [PMOD p]`: `a` and `b` are congruent modulo a`p`.

## See also

`SModEq` is a generalisation to arbitrary submodules.

## TODO

Delete `Int.ModEq` in favour of `AddCommGroup.ModEq`. Generalise `SModEq` to `AddSubgroup` and
redefine `AddCommGroup.ModEq` using it. Once this is done, we can rename `AddCommGroup.ModEq`
to `AddSubgroup.ModEq` and multiplicativise it. Longer term, we could generalise to submonoids and
also unify with `Nat.ModEq`.
-/


namespace AddCommGroup

variable {Œ± : Type*}

section AddCommGroup

variable [AddCommGroup Œ±] {p a a‚ÇÅ a‚ÇÇ b b‚ÇÅ b‚ÇÇ c : Œ±} {n : ‚Ñï} {z : ‚Ñ§}

/-- `a ‚â° b [PMOD p]` means that `b` is congruent to `a` modulo `p`.

Equivalently (as shown in `Algebra.Order.ToIntervalMod`), `b` does not lie in the open interval
`(a, a + p)` modulo `p`, or `toIcoMod hp a` disagrees with `toIocMod hp a` at `b`, or
`toIcoDiv hp a` disagrees with `toIocDiv hp a` at `b`. -/
def ModEq (p a b : Œ±) : Prop :=
  ‚àÉ z : ‚Ñ§, b - a = z ‚Ä¢ p
#align add_comm_group.modeq AddCommGroup.ModEq

notation:50 a " ‚â° " b " [PMOD " p "]" => ModEq p a b

@[refl, simp]
theorem modEq_refl (a : Œ±) : a ‚â° a [PMOD p] :=
  ‚ü®0, by simp‚ü©
         -- üéâ no goals
#align add_comm_group.modeq_refl AddCommGroup.modEq_refl

theorem modEq_rfl : a ‚â° a [PMOD p] :=
  modEq_refl _
#align add_comm_group.modeq_rfl AddCommGroup.modEq_rfl

theorem modEq_comm : a ‚â° b [PMOD p] ‚Üî b ‚â° a [PMOD p] :=
  (Equiv.neg _).exists_congr_left.trans <| by simp [ModEq, ‚Üê neg_eq_iff_eq_neg]
                                              -- üéâ no goals
#align add_comm_group.modeq_comm AddCommGroup.modEq_comm

alias ‚ü®ModEq.symm, _‚ü© := modEq_comm
#align add_comm_group.modeq.symm AddCommGroup.ModEq.symm

attribute [symm] ModEq.symm

@[trans]
theorem ModEq.trans : a ‚â° b [PMOD p] ‚Üí b ‚â° c [PMOD p] ‚Üí a ‚â° c [PMOD p] := fun ‚ü®m, hm‚ü© ‚ü®n, hn‚ü© =>
  ‚ü®m + n, by simp [add_smul, ‚Üê hm, ‚Üê hn]‚ü©
             -- üéâ no goals
#align add_comm_group.modeq.trans AddCommGroup.ModEq.trans

instance : IsRefl _ (ModEq p) :=
  ‚ü®modEq_refl‚ü©

@[simp]
theorem neg_modEq_neg : -a ‚â° -b [PMOD p] ‚Üî a ‚â° b [PMOD p] :=
  modEq_comm.trans <| by simp [ModEq, neg_add_eq_sub]
                         -- üéâ no goals
#align add_comm_group.neg_modeq_neg AddCommGroup.neg_modEq_neg

alias ‚ü®ModEq.of_neg, ModEq.neg‚ü© := neg_modEq_neg
#align add_comm_group.modeq.of_neg AddCommGroup.ModEq.of_neg
#align add_comm_group.modeq.neg AddCommGroup.ModEq.neg

@[simp]
theorem modEq_neg : a ‚â° b [PMOD -p] ‚Üî a ‚â° b [PMOD p] :=
  modEq_comm.trans <| by simp [ModEq, ‚Üê neg_eq_iff_eq_neg]
                         -- üéâ no goals
#align add_comm_group.modeq_neg AddCommGroup.modEq_neg

alias ‚ü®ModEq.of_neg', ModEq.neg'‚ü© := modEq_neg
#align add_comm_group.modeq.of_neg' AddCommGroup.ModEq.of_neg'
#align add_comm_group.modeq.neg' AddCommGroup.ModEq.neg'

theorem modEq_sub (a b : Œ±) : a ‚â° b [PMOD b - a] :=
  ‚ü®1, (one_smul _ _).symm‚ü©
#align add_comm_group.modeq_sub AddCommGroup.modEq_sub

@[simp]
theorem modEq_zero : a ‚â° b [PMOD 0] ‚Üî a = b := by simp [ModEq, sub_eq_zero, eq_comm]
                                                  -- üéâ no goals
#align add_comm_group.modeq_zero AddCommGroup.modEq_zero

@[simp]
theorem self_modEq_zero : p ‚â° 0 [PMOD p] :=
  ‚ü®-1, by simp‚ü©
          -- üéâ no goals
#align add_comm_group.self_modeq_zero AddCommGroup.self_modEq_zero

@[simp]
theorem zsmul_modEq_zero (z : ‚Ñ§) : z ‚Ä¢ p ‚â° 0 [PMOD p] :=
  ‚ü®-z, by simp‚ü©
          -- üéâ no goals
#align add_comm_group.zsmul_modeq_zero AddCommGroup.zsmul_modEq_zero

theorem add_zsmul_modEq (z : ‚Ñ§) : a + z ‚Ä¢ p ‚â° a [PMOD p] :=
  ‚ü®-z, by simp‚ü©
          -- üéâ no goals
#align add_comm_group.add_zsmul_modeq AddCommGroup.add_zsmul_modEq

theorem zsmul_add_modEq (z : ‚Ñ§) : z ‚Ä¢ p + a ‚â° a [PMOD p] :=
  ‚ü®-z, by simp [‚Üê sub_sub]‚ü©
          -- üéâ no goals
#align add_comm_group.zsmul_add_modeq AddCommGroup.zsmul_add_modEq

theorem add_nsmul_modEq (n : ‚Ñï) : a + n ‚Ä¢ p ‚â° a [PMOD p] :=
  ‚ü®-n, by simp‚ü©
          -- üéâ no goals
#align add_comm_group.add_nsmul_modeq AddCommGroup.add_nsmul_modEq

theorem nsmul_add_modEq (n : ‚Ñï) : n ‚Ä¢ p + a ‚â° a [PMOD p] :=
  ‚ü®-n, by simp [‚Üê sub_sub]‚ü©
          -- üéâ no goals
#align add_comm_group.nsmul_add_modeq AddCommGroup.nsmul_add_modEq

namespace ModEq

protected theorem add_zsmul (z : ‚Ñ§) : a ‚â° b [PMOD p] ‚Üí a + z ‚Ä¢ p ‚â° b [PMOD p] :=
  (add_zsmul_modEq _).trans
#align add_comm_group.modeq.add_zsmul AddCommGroup.ModEq.add_zsmul

protected theorem zsmul_add (z : ‚Ñ§) : a ‚â° b [PMOD p] ‚Üí z ‚Ä¢ p + a ‚â° b [PMOD p] :=
  (zsmul_add_modEq _).trans
#align add_comm_group.modeq.zsmul_add AddCommGroup.ModEq.zsmul_add

protected theorem add_nsmul (n : ‚Ñï) : a ‚â° b [PMOD p] ‚Üí a + n ‚Ä¢ p ‚â° b [PMOD p] :=
  (add_nsmul_modEq _).trans
#align add_comm_group.modeq.add_nsmul AddCommGroup.ModEq.add_nsmul

protected theorem nsmul_add (n : ‚Ñï) : a ‚â° b [PMOD p] ‚Üí n ‚Ä¢ p + a ‚â° b [PMOD p] :=
  (nsmul_add_modEq _).trans
#align add_comm_group.modeq.nsmul_add AddCommGroup.ModEq.nsmul_add

protected theorem of_zsmul : a ‚â° b [PMOD z ‚Ä¢ p] ‚Üí a ‚â° b [PMOD p] := fun ‚ü®m, hm‚ü© =>
  ‚ü®m * z, by rwa [mul_smul]‚ü©
             -- üéâ no goals
#align add_comm_group.modeq.of_zsmul AddCommGroup.ModEq.of_zsmul

protected theorem of_nsmul : a ‚â° b [PMOD n ‚Ä¢ p] ‚Üí a ‚â° b [PMOD p] := fun ‚ü®m, hm‚ü© =>
  ‚ü®m * n, by rwa [mul_smul, coe_nat_zsmul]‚ü©
             -- üéâ no goals
#align add_comm_group.modeq.of_nsmul AddCommGroup.ModEq.of_nsmul

protected theorem zsmul : a ‚â° b [PMOD p] ‚Üí z ‚Ä¢ a ‚â° z ‚Ä¢ b [PMOD z ‚Ä¢ p] :=
  Exists.imp fun m hm => by rw [‚Üê smul_sub, hm, smul_comm]
                            -- üéâ no goals
#align add_comm_group.modeq.zsmul AddCommGroup.ModEq.zsmul

protected theorem nsmul : a ‚â° b [PMOD p] ‚Üí n ‚Ä¢ a ‚â° n ‚Ä¢ b [PMOD n ‚Ä¢ p] :=
  Exists.imp fun m hm => by rw [‚Üê smul_sub, hm, smul_comm]
                            -- üéâ no goals
#align add_comm_group.modeq.nsmul AddCommGroup.ModEq.nsmul

end ModEq

@[simp]
theorem zsmul_modEq_zsmul [NoZeroSMulDivisors ‚Ñ§ Œ±] (hn : z ‚â† 0) :
    z ‚Ä¢ a ‚â° z ‚Ä¢ b [PMOD z ‚Ä¢ p] ‚Üî a ‚â° b [PMOD p] :=
  exists_congr fun m => by rw [‚Üê smul_sub, smul_comm, smul_right_inj hn]
                           -- üéâ no goals
#align add_comm_group.zsmul_modeq_zsmul AddCommGroup.zsmul_modEq_zsmul

@[simp]
theorem nsmul_modEq_nsmul [NoZeroSMulDivisors ‚Ñï Œ±] (hn : n ‚â† 0) :
    n ‚Ä¢ a ‚â° n ‚Ä¢ b [PMOD n ‚Ä¢ p] ‚Üî a ‚â° b [PMOD p] :=
  exists_congr fun m => by rw [‚Üê smul_sub, smul_comm, smul_right_inj hn]
                           -- üéâ no goals
#align add_comm_group.nsmul_modeq_nsmul AddCommGroup.nsmul_modEq_nsmul

alias ‚ü®ModEq.zsmul_cancel, _‚ü© := zsmul_modEq_zsmul
#align add_comm_group.modeq.zsmul_cancel AddCommGroup.ModEq.zsmul_cancel

alias ‚ü®ModEq.nsmul_cancel, _‚ü© := nsmul_modEq_nsmul
#align add_comm_group.modeq.nsmul_cancel AddCommGroup.ModEq.nsmul_cancel

namespace ModEq

@[simp]
protected theorem add_iff_left :
    a‚ÇÅ ‚â° b‚ÇÅ [PMOD p] ‚Üí (a‚ÇÅ + a‚ÇÇ ‚â° b‚ÇÅ + b‚ÇÇ [PMOD p] ‚Üî a‚ÇÇ ‚â° b‚ÇÇ [PMOD p]) := fun ‚ü®m, hm‚ü© =>
  (Equiv.addLeft m).symm.exists_congr_left.trans <| by simp [add_sub_add_comm, hm, add_smul, ModEq]
                                                       -- üéâ no goals
#align add_comm_group.modeq.add_iff_left AddCommGroup.ModEq.add_iff_left

@[simp]
protected theorem add_iff_right :
    a‚ÇÇ ‚â° b‚ÇÇ [PMOD p] ‚Üí (a‚ÇÅ + a‚ÇÇ ‚â° b‚ÇÅ + b‚ÇÇ [PMOD p] ‚Üî a‚ÇÅ ‚â° b‚ÇÅ [PMOD p]) := fun ‚ü®m, hm‚ü© =>
  (Equiv.addRight m).symm.exists_congr_left.trans <| by simp [add_sub_add_comm, hm, add_smul, ModEq]
                                                        -- üéâ no goals
#align add_comm_group.modeq.add_iff_right AddCommGroup.ModEq.add_iff_right

@[simp]
protected theorem sub_iff_left :
    a‚ÇÅ ‚â° b‚ÇÅ [PMOD p] ‚Üí (a‚ÇÅ - a‚ÇÇ ‚â° b‚ÇÅ - b‚ÇÇ [PMOD p] ‚Üî a‚ÇÇ ‚â° b‚ÇÇ [PMOD p]) := fun ‚ü®m, hm‚ü© =>
  (Equiv.subLeft m).symm.exists_congr_left.trans <| by simp [sub_sub_sub_comm, hm, sub_smul, ModEq]
                                                       -- üéâ no goals
#align add_comm_group.modeq.sub_iff_left AddCommGroup.ModEq.sub_iff_left

@[simp]
protected theorem sub_iff_right :
    a‚ÇÇ ‚â° b‚ÇÇ [PMOD p] ‚Üí (a‚ÇÅ - a‚ÇÇ ‚â° b‚ÇÅ - b‚ÇÇ [PMOD p] ‚Üî a‚ÇÅ ‚â° b‚ÇÅ [PMOD p]) := fun ‚ü®m, hm‚ü© =>
  (Equiv.subRight m).symm.exists_congr_left.trans <| by simp [sub_sub_sub_comm, hm, sub_smul, ModEq]
                                                        -- üéâ no goals
#align add_comm_group.modeq.sub_iff_right AddCommGroup.ModEq.sub_iff_right

alias ‚ü®add_left_cancel, add‚ü© := ModEq.add_iff_left
#align add_comm_group.modeq.add_left_cancel AddCommGroup.ModEq.add_left_cancel
#align add_comm_group.modeq.add AddCommGroup.ModEq.add

alias ‚ü®add_right_cancel, _‚ü© := ModEq.add_iff_right
#align add_comm_group.modeq.add_right_cancel AddCommGroup.ModEq.add_right_cancel

alias ‚ü®sub_left_cancel, sub‚ü© := ModEq.sub_iff_left
#align add_comm_group.modeq.sub_left_cancel AddCommGroup.ModEq.sub_left_cancel
#align add_comm_group.modeq.sub AddCommGroup.ModEq.sub

alias ‚ü®sub_right_cancel, _‚ü© := ModEq.sub_iff_right
#align add_comm_group.modeq.sub_right_cancel AddCommGroup.ModEq.sub_right_cancel

-- porting note: doesn't work
-- attribute [protected] add_left_cancel add_right_cancel add sub_left_cancel sub_right_cancel sub

protected theorem add_left (c : Œ±) (h : a ‚â° b [PMOD p]) : c + a ‚â° c + b [PMOD p] :=
  modEq_rfl.add h
#align add_comm_group.modeq.add_left AddCommGroup.ModEq.add_left

protected theorem sub_left (c : Œ±) (h : a ‚â° b [PMOD p]) : c - a ‚â° c - b [PMOD p] :=
  modEq_rfl.sub h
#align add_comm_group.modeq.sub_left AddCommGroup.ModEq.sub_left

protected theorem add_right (c : Œ±) (h : a ‚â° b [PMOD p]) : a + c ‚â° b + c [PMOD p] :=
  h.add modEq_rfl
#align add_comm_group.modeq.add_right AddCommGroup.ModEq.add_right

protected theorem sub_right (c : Œ±) (h : a ‚â° b [PMOD p]) : a - c ‚â° b - c [PMOD p] :=
  h.sub modEq_rfl
#align add_comm_group.modeq.sub_right AddCommGroup.ModEq.sub_right

protected theorem add_left_cancel' (c : Œ±) : c + a ‚â° c + b [PMOD p] ‚Üí a ‚â° b [PMOD p] :=
  modEq_rfl.add_left_cancel
#align add_comm_group.modeq.add_left_cancel' AddCommGroup.ModEq.add_left_cancel'

protected theorem add_right_cancel' (c : Œ±) : a + c ‚â° b + c [PMOD p] ‚Üí a ‚â° b [PMOD p] :=
  modEq_rfl.add_right_cancel
#align add_comm_group.modeq.add_right_cancel' AddCommGroup.ModEq.add_right_cancel'

protected theorem sub_left_cancel' (c : Œ±) : c - a ‚â° c - b [PMOD p] ‚Üí a ‚â° b [PMOD p] :=
  modEq_rfl.sub_left_cancel
#align add_comm_group.modeq.sub_left_cancel' AddCommGroup.ModEq.sub_left_cancel'

protected theorem sub_right_cancel' (c : Œ±) : a - c ‚â° b - c [PMOD p] ‚Üí a ‚â° b [PMOD p] :=
  modEq_rfl.sub_right_cancel
#align add_comm_group.modeq.sub_right_cancel' AddCommGroup.ModEq.sub_right_cancel'

end ModEq

theorem modEq_sub_iff_add_modEq' : a ‚â° b - c [PMOD p] ‚Üî c + a ‚â° b [PMOD p] := by
  simp [ModEq, sub_sub]
  -- üéâ no goals
#align add_comm_group.modeq_sub_iff_add_modeq' AddCommGroup.modEq_sub_iff_add_modEq'

theorem modEq_sub_iff_add_modEq : a ‚â° b - c [PMOD p] ‚Üî a + c ‚â° b [PMOD p] :=
  modEq_sub_iff_add_modEq'.trans <| by rw [add_comm]
                                       -- üéâ no goals
#align add_comm_group.modeq_sub_iff_add_modeq AddCommGroup.modEq_sub_iff_add_modEq

theorem sub_modEq_iff_modEq_add' : a - b ‚â° c [PMOD p] ‚Üî a ‚â° b + c [PMOD p] :=
  modEq_comm.trans <| modEq_sub_iff_add_modEq'.trans modEq_comm
#align add_comm_group.sub_modeq_iff_modeq_add' AddCommGroup.sub_modEq_iff_modEq_add'

theorem sub_modEq_iff_modEq_add : a - b ‚â° c [PMOD p] ‚Üî a ‚â° c + b [PMOD p] :=
  modEq_comm.trans <| modEq_sub_iff_add_modEq.trans modEq_comm
#align add_comm_group.sub_modeq_iff_modeq_add AddCommGroup.sub_modEq_iff_modEq_add

@[simp]
theorem sub_modEq_zero : a - b ‚â° 0 [PMOD p] ‚Üî a ‚â° b [PMOD p] := by simp [sub_modEq_iff_modEq_add]
                                                                   -- üéâ no goals
#align add_comm_group.sub_modeq_zero AddCommGroup.sub_modEq_zero

@[simp]
theorem add_modEq_left : a + b ‚â° a [PMOD p] ‚Üî b ‚â° 0 [PMOD p] := by simp [‚Üê modEq_sub_iff_add_modEq']
                                                                   -- üéâ no goals
#align add_comm_group.add_modeq_left AddCommGroup.add_modEq_left

@[simp]
theorem add_modEq_right : a + b ‚â° b [PMOD p] ‚Üî a ‚â° 0 [PMOD p] := by simp [‚Üê modEq_sub_iff_add_modEq]
                                                                    -- üéâ no goals
#align add_comm_group.add_modeq_right AddCommGroup.add_modEq_right

theorem modEq_iff_eq_add_zsmul : a ‚â° b [PMOD p] ‚Üî ‚àÉ z : ‚Ñ§, b = a + z ‚Ä¢ p := by
  simp_rw [ModEq, sub_eq_iff_eq_add']
  -- üéâ no goals
#align add_comm_group.modeq_iff_eq_add_zsmul AddCommGroup.modEq_iff_eq_add_zsmul

theorem not_modEq_iff_ne_add_zsmul : ¬¨a ‚â° b [PMOD p] ‚Üî ‚àÄ z : ‚Ñ§, b ‚â† a + z ‚Ä¢ p := by
  rw [modEq_iff_eq_add_zsmul, not_exists]
  -- üéâ no goals
#align add_comm_group.not_modeq_iff_ne_add_zsmul AddCommGroup.not_modEq_iff_ne_add_zsmul

theorem modEq_iff_eq_mod_zmultiples : a ‚â° b [PMOD p] ‚Üî (b : Œ± ‚ß∏ AddSubgroup.zmultiples p) = a := by
  simp_rw [modEq_iff_eq_add_zsmul, QuotientAddGroup.eq_iff_sub_mem, AddSubgroup.mem_zmultiples_iff,
    eq_sub_iff_add_eq', eq_comm]
#align add_comm_group.modeq_iff_eq_mod_zmultiples AddCommGroup.modEq_iff_eq_mod_zmultiples

theorem not_modEq_iff_ne_mod_zmultiples :
    ¬¨a ‚â° b [PMOD p] ‚Üî (b : Œ± ‚ß∏ AddSubgroup.zmultiples p) ‚â† a :=
  modEq_iff_eq_mod_zmultiples.not
#align add_comm_group.not_modeq_iff_ne_mod_zmultiples AddCommGroup.not_modEq_iff_ne_mod_zmultiples

end AddCommGroup

@[simp]
theorem modEq_iff_int_modEq {a b z : ‚Ñ§} : a ‚â° b [PMOD z] ‚Üî a ‚â° b [ZMOD z] := by
  simp [ModEq, dvd_iff_exists_eq_mul_left, Int.modEq_iff_dvd]
  -- üéâ no goals
#align add_comm_group.modeq_iff_int_modeq AddCommGroup.modEq_iff_int_modEq

section AddCommGroupWithOne

variable [AddCommGroupWithOne Œ±] [CharZero Œ±]

@[simp, norm_cast]
theorem int_cast_modEq_int_cast {a b z : ‚Ñ§} : a ‚â° b [PMOD (z : Œ±)] ‚Üî a ‚â° b [PMOD z] := by
  simp_rw [ModEq, ‚Üê Int.cast_mul_eq_zsmul_cast]
  -- ‚ä¢ (‚àÉ z_1, ‚Üëb - ‚Üëa = ‚Üë(z_1 * z)) ‚Üî ‚àÉ z_1, b - a = z_1 ‚Ä¢ z
  norm_cast
  -- üéâ no goals
#align add_comm_group.int_cast_modeq_int_cast AddCommGroup.int_cast_modEq_int_cast

@[simp, norm_cast]
theorem nat_cast_modEq_nat_cast {a b n : ‚Ñï} : a ‚â° b [PMOD (n : Œ±)] ‚Üî a ‚â° b [MOD n] := by
  simp_rw [‚Üê Int.coe_nat_modEq_iff, ‚Üê modEq_iff_int_modEq, ‚Üê @int_cast_modEq_int_cast Œ±,
    Int.cast_ofNat]
#align add_comm_group.nat_cast_modeq_nat_cast AddCommGroup.nat_cast_modEq_nat_cast

alias ‚ü®ModEq.of_int_cast, ModEq.int_cast‚ü© := int_cast_modEq_int_cast
#align add_comm_group.modeq.of_int_cast AddCommGroup.ModEq.of_int_cast
#align add_comm_group.modeq.int_cast AddCommGroup.ModEq.int_cast

alias ‚ü®_root_.Nat.ModEq.of_nat_cast, ModEq.nat_cast‚ü© := nat_cast_modEq_nat_cast
#align nat.modeq.of_nat_cast Nat.ModEq.of_nat_cast
#align add_comm_group.modeq.nat_cast AddCommGroup.ModEq.nat_cast

end AddCommGroupWithOne

end AddCommGroup
