/-
Copyright (c) 2020 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou
-/
import Mathlib.Algebra.Support

#align_import algebra.indicator_function from "leanprover-community/mathlib"@"2445c98ae4b87eabebdde552593519b9b6dc350c"

/-!
# Indicator function

- `Set.indicator (s : Set Î±) (f : Î± â†’ Î²) (a : Î±)` is `f a` if `a âˆˆ s` and is `0` otherwise.
- `Set.mulIndicator (s : Set Î±) (f : Î± â†’ Î²) (a : Î±)` is `f a` if `a âˆˆ s` and is `1` otherwise.


## Implementation note

In mathematics, an indicator function or a characteristic function is a function
used to indicate membership of an element in a set `s`,
having the value `1` for all elements of `s` and the value `0` otherwise.
But since it is usually used to restrict a function to a certain set `s`,
we let the indicator function take the value `f x` for some function `f`, instead of `1`.
If the usual indicator function is needed, just set `f` to be the constant function `fun _ â†¦ 1`.

The indicator function is implemented non-computably, to avoid having to pass around `Decidable`
arguments. This is in contrast with the design of `Pi.single` or `Set.piecewise`.

## Tags
indicator, characteristic
-/

open BigOperators

open Function

variable {Î± Î² Î¹ M N : Type*}

namespace Set

section One

variable [One M] [One N] {s t : Set Î±} {f g : Î± â†’ M} {a : Î±}

/-- `Set.mulIndicator s f a` is `f a` if `a âˆˆ s`, `1` otherwise.  -/
@[to_additive "`Set.indicator s f a` is `f a` if `a âˆˆ s`, `0` otherwise."]
noncomputable def mulIndicator (s : Set Î±) (f : Î± â†’ M) (x : Î±) : M :=
  haveI := Classical.decPred (Â· âˆˆ s)
  if x âˆˆ s then f x else 1
#align set.mul_indicator Set.mulIndicator

@[to_additive (attr := simp)]
theorem piecewise_eq_mulIndicator [DecidablePred (Â· âˆˆ s)] : s.piecewise f 1 = s.mulIndicator f :=
  funext fun _ => @if_congr _ _ _ _ (id _) _ _ _ _ Iff.rfl rfl rfl
#align set.piecewise_eq_mul_indicator Set.piecewise_eq_mulIndicator
#align set.piecewise_eq_indicator Set.piecewise_eq_indicator

-- Porting note: needed unfold for mulIndicator
@[to_additive]
theorem mulIndicator_apply (s : Set Î±) (f : Î± â†’ M) (a : Î±) [Decidable (a âˆˆ s)] :
    mulIndicator s f a = if a âˆˆ s then f a else 1 := by
  unfold mulIndicator
  -- âŠ¢ (if a âˆˆ s then f a else 1) = if a âˆˆ s then f a else 1
  congr
  -- ğŸ‰ no goals
#align set.mul_indicator_apply Set.mulIndicator_apply
#align set.indicator_apply Set.indicator_apply

@[to_additive (attr := simp)]
theorem mulIndicator_of_mem (h : a âˆˆ s) (f : Î± â†’ M) : mulIndicator s f a = f a :=
  if_pos h
#align set.mul_indicator_of_mem Set.mulIndicator_of_mem
#align set.indicator_of_mem Set.indicator_of_mem

@[to_additive (attr := simp)]
theorem mulIndicator_of_not_mem (h : a âˆ‰ s) (f : Î± â†’ M) : mulIndicator s f a = 1 :=
  if_neg h
#align set.mul_indicator_of_not_mem Set.mulIndicator_of_not_mem
#align set.indicator_of_not_mem Set.indicator_of_not_mem

@[to_additive]
theorem mulIndicator_eq_one_or_self (s : Set Î±) (f : Î± â†’ M) (a : Î±) :
    mulIndicator s f a = 1 âˆ¨ mulIndicator s f a = f a := by
  by_cases h : a âˆˆ s
  -- âŠ¢ mulIndicator s f a = 1 âˆ¨ mulIndicator s f a = f a
  Â· exact Or.inr (mulIndicator_of_mem h f)
    -- ğŸ‰ no goals
  Â· exact Or.inl (mulIndicator_of_not_mem h f)
    -- ğŸ‰ no goals
#align set.mul_indicator_eq_one_or_self Set.mulIndicator_eq_one_or_self
#align set.indicator_eq_zero_or_self Set.indicator_eq_zero_or_self

@[to_additive (attr := simp)]
theorem mulIndicator_apply_eq_self : s.mulIndicator f a = f a â†” a âˆ‰ s â†’ f a = 1 :=
  letI := Classical.dec (a âˆˆ s)
  ite_eq_left_iff.trans (by rw [@eq_comm _ (f a)])
                            -- ğŸ‰ no goals
#align set.mul_indicator_apply_eq_self Set.mulIndicator_apply_eq_self
#align set.indicator_apply_eq_self Set.indicator_apply_eq_self

@[to_additive (attr := simp)]
theorem mulIndicator_eq_self : s.mulIndicator f = f â†” mulSupport f âŠ† s := by
  simp only [funext_iff, subset_def, mem_mulSupport, mulIndicator_apply_eq_self, not_imp_comm]
  -- ğŸ‰ no goals
#align set.mul_indicator_eq_self Set.mulIndicator_eq_self
#align set.indicator_eq_self Set.indicator_eq_self

@[to_additive]
theorem mulIndicator_eq_self_of_superset (h1 : s.mulIndicator f = f) (h2 : s âŠ† t) :
    t.mulIndicator f = f := by
  rw [mulIndicator_eq_self] at h1 âŠ¢
  -- âŠ¢ mulSupport f âŠ† t
  exact Subset.trans h1 h2
  -- ğŸ‰ no goals
#align set.mul_indicator_eq_self_of_superset Set.mulIndicator_eq_self_of_superset
#align set.indicator_eq_self_of_superset Set.indicator_eq_self_of_superset

@[to_additive (attr := simp)]
theorem mulIndicator_apply_eq_one : mulIndicator s f a = 1 â†” a âˆˆ s â†’ f a = 1 :=
  letI := Classical.dec (a âˆˆ s)
  ite_eq_right_iff
#align set.mul_indicator_apply_eq_one Set.mulIndicator_apply_eq_one
#align set.indicator_apply_eq_zero Set.indicator_apply_eq_zero

@[to_additive (attr := simp)]
theorem mulIndicator_eq_one : (mulIndicator s f = fun x => 1) â†” Disjoint (mulSupport f) s := by
  simp only [funext_iff, mulIndicator_apply_eq_one, Set.disjoint_left, mem_mulSupport,
    not_imp_not]
#align set.mul_indicator_eq_one Set.mulIndicator_eq_one
#align set.indicator_eq_zero Set.indicator_eq_zero

@[to_additive (attr := simp)]
theorem mulIndicator_eq_one' : mulIndicator s f = 1 â†” Disjoint (mulSupport f) s :=
  mulIndicator_eq_one
#align set.mul_indicator_eq_one' Set.mulIndicator_eq_one'
#align set.indicator_eq_zero' Set.indicator_eq_zero'

@[to_additive]
theorem mulIndicator_apply_ne_one {a : Î±} : s.mulIndicator f a â‰  1 â†” a âˆˆ s âˆ© mulSupport f := by
  simp only [Ne.def, mulIndicator_apply_eq_one, not_imp, mem_inter_iff, mem_mulSupport]
  -- ğŸ‰ no goals
#align set.mul_indicator_apply_ne_one Set.mulIndicator_apply_ne_one
#align set.indicator_apply_ne_zero Set.indicator_apply_ne_zero

@[to_additive (attr := simp)]
theorem mulSupport_mulIndicator :
    Function.mulSupport (s.mulIndicator f) = s âˆ© Function.mulSupport f :=
  ext fun x => by simp [Function.mem_mulSupport, mulIndicator_apply_eq_one]
                  -- ğŸ‰ no goals
#align set.mul_support_mul_indicator Set.mulSupport_mulIndicator
#align set.support_indicator Set.support_indicator

/-- If a multiplicative indicator function is not equal to `1` at a point, then that point is in the
set. -/
@[to_additive
      "If an additive indicator function is not equal to `0` at a point, then that point is
      in the set."]
theorem mem_of_mulIndicator_ne_one (h : mulIndicator s f a â‰  1) : a âˆˆ s :=
  not_imp_comm.1 (fun hn => mulIndicator_of_not_mem hn f) h
#align set.mem_of_mul_indicator_ne_one Set.mem_of_mulIndicator_ne_one
#align set.mem_of_indicator_ne_zero Set.mem_of_indicator_ne_zero

@[to_additive]
theorem eqOn_mulIndicator : EqOn (mulIndicator s f) f s := fun _ hx => mulIndicator_of_mem hx f
#align set.eq_on_mul_indicator Set.eqOn_mulIndicator
#align set.eq_on_indicator Set.eqOn_indicator

@[to_additive]
theorem mulSupport_mulIndicator_subset : mulSupport (s.mulIndicator f) âŠ† s := fun _ hx =>
  hx.imp_symm fun h => mulIndicator_of_not_mem h f
#align set.mul_support_mul_indicator_subset Set.mulSupport_mulIndicator_subset
#align set.support_indicator_subset Set.support_indicator_subset

@[to_additive (attr := simp)]
theorem mulIndicator_mulSupport : mulIndicator (mulSupport f) f = f :=
  mulIndicator_eq_self.2 Subset.rfl
#align set.mul_indicator_mul_support Set.mulIndicator_mulSupport
#align set.indicator_support Set.indicator_support

@[to_additive (attr := simp)]
theorem mulIndicator_range_comp {Î¹ : Sort*} (f : Î¹ â†’ Î±) (g : Î± â†’ M) :
    mulIndicator (range f) g âˆ˜ f = g âˆ˜ f :=
  letI := Classical.decPred (Â· âˆˆ range f)
  piecewise_range_comp _ _ _
#align set.mul_indicator_range_comp Set.mulIndicator_range_comp
#align set.indicator_range_comp Set.indicator_range_comp

@[to_additive]
theorem mulIndicator_congr (h : EqOn f g s) : mulIndicator s f = mulIndicator s g :=
  funext fun x => by
    simp only [mulIndicator]
    -- âŠ¢ (if x âˆˆ s then f x else 1) = if x âˆˆ s then g x else 1
    split_ifs with h_1
    -- âŠ¢ f x = g x
    Â· exact h h_1
      -- ğŸ‰ no goals
    rfl
    -- ğŸ‰ no goals
#align set.mul_indicator_congr Set.mulIndicator_congr
#align set.indicator_congr Set.indicator_congr

@[to_additive (attr := simp)]
theorem mulIndicator_univ (f : Î± â†’ M) : mulIndicator (univ : Set Î±) f = f :=
  mulIndicator_eq_self.2 <| subset_univ _
#align set.mul_indicator_univ Set.mulIndicator_univ
#align set.indicator_univ Set.indicator_univ

@[to_additive (attr := simp)]
theorem mulIndicator_empty (f : Î± â†’ M) : mulIndicator (âˆ… : Set Î±) f = fun _ => 1 :=
  mulIndicator_eq_one.2 <| disjoint_empty _
#align set.mul_indicator_empty Set.mulIndicator_empty
#align set.indicator_empty Set.indicator_empty

@[to_additive]
theorem mulIndicator_empty' (f : Î± â†’ M) : mulIndicator (âˆ… : Set Î±) f = 1 :=
  mulIndicator_empty f
#align set.mul_indicator_empty' Set.mulIndicator_empty'
#align set.indicator_empty' Set.indicator_empty'

variable (M)

@[to_additive (attr := simp)]
theorem mulIndicator_one (s : Set Î±) : (mulIndicator s fun _ => (1 : M)) = fun _ => (1 : M) :=
  mulIndicator_eq_one.2 <| by simp only [mulSupport_one, empty_disjoint]
                              -- ğŸ‰ no goals
#align set.mul_indicator_one Set.mulIndicator_one
#align set.indicator_zero Set.indicator_zero

@[to_additive (attr := simp)]
theorem mulIndicator_one' {s : Set Î±} : s.mulIndicator (1 : Î± â†’ M) = 1 :=
  mulIndicator_one M s
#align set.mul_indicator_one' Set.mulIndicator_one'
#align set.indicator_zero' Set.indicator_zero'

variable {M}

@[to_additive]
theorem mulIndicator_mulIndicator (s t : Set Î±) (f : Î± â†’ M) :
    mulIndicator s (mulIndicator t f) = mulIndicator (s âˆ© t) f :=
  funext fun x => by
    simp only [mulIndicator]
    -- âŠ¢ (if x âˆˆ s then if x âˆˆ t then f x else 1 else 1) = if x âˆˆ s âˆ© t then f x else 1
    split_ifs <;> simp_all (config := { contextual := true })
                  -- ğŸ‰ no goals
                  -- ğŸ‰ no goals
                  -- ğŸ‰ no goals
                  -- ğŸ‰ no goals
                  -- ğŸ‰ no goals
                  -- ğŸ‰ no goals
#align set.mul_indicator_mul_indicator Set.mulIndicator_mulIndicator
#align set.indicator_indicator Set.indicator_indicator

@[to_additive (attr := simp)]
theorem mulIndicator_inter_mulSupport (s : Set Î±) (f : Î± â†’ M) :
    mulIndicator (s âˆ© mulSupport f) f = mulIndicator s f := by
  rw [â† mulIndicator_mulIndicator, mulIndicator_mulSupport]
  -- ğŸ‰ no goals
#align set.mul_indicator_inter_mul_support Set.mulIndicator_inter_mulSupport
#align set.indicator_inter_support Set.indicator_inter_support

@[to_additive]
theorem comp_mulIndicator (h : M â†’ Î²) (f : Î± â†’ M) {s : Set Î±} {x : Î±} [DecidablePred (Â· âˆˆ s)] :
    h (s.mulIndicator f x) = s.piecewise (h âˆ˜ f) (const Î± (h 1)) x := by
  letI := Classical.decPred (Â· âˆˆ s)
  -- âŠ¢ h (mulIndicator s f x) = piecewise s (h âˆ˜ f) (const Î± (h 1)) x
  convert s.apply_piecewise f (const Î± 1) (fun _ => h) (x := x) using 2
  -- ğŸ‰ no goals
#align set.comp_mul_indicator Set.comp_mulIndicator
#align set.comp_indicator Set.comp_indicator

@[to_additive]
theorem mulIndicator_comp_right {s : Set Î±} (f : Î² â†’ Î±) {g : Î± â†’ M} {x : Î²} :
    mulIndicator (f â»Â¹' s) (g âˆ˜ f) x = mulIndicator s g (f x) := by
  simp only [mulIndicator, Function.comp]
  -- âŠ¢ (if x âˆˆ f â»Â¹' s then g (f x) else 1) = if f x âˆˆ s then g (f x) else 1
  split_ifs with h h' h'' <;> first | rfl | contradiction
                              -- ğŸ‰ no goals
                              -- ğŸ‰ no goals
                              -- ğŸ‰ no goals
                              -- ğŸ‰ no goals
#align set.mul_indicator_comp_right Set.mulIndicator_comp_right
#align set.indicator_comp_right Set.indicator_comp_right

@[to_additive]
theorem mulIndicator_image {s : Set Î±} {f : Î² â†’ M} {g : Î± â†’ Î²} (hg : Injective g) {x : Î±} :
    mulIndicator (g '' s) f (g x) = mulIndicator s (f âˆ˜ g) x := by
  rw [â† mulIndicator_comp_right, preimage_image_eq _ hg]
  -- ğŸ‰ no goals
#align set.mul_indicator_image Set.mulIndicator_image
#align set.indicator_image Set.indicator_image

@[to_additive]
theorem mulIndicator_comp_of_one {g : M â†’ N} (hg : g 1 = 1) :
    mulIndicator s (g âˆ˜ f) = g âˆ˜ mulIndicator s f := by
  funext
  -- âŠ¢ mulIndicator s (g âˆ˜ f) xâœ = (g âˆ˜ mulIndicator s f) xâœ
  simp only [mulIndicator]
  -- âŠ¢ (if xâœ âˆˆ s then (g âˆ˜ f) xâœ else 1) = (g âˆ˜ mulIndicator s f) xâœ
  split_ifs <;> simp [*]
  -- âŠ¢ (g âˆ˜ f) xâœ = (g âˆ˜ mulIndicator s f) xâœ
                -- ğŸ‰ no goals
                -- ğŸ‰ no goals
#align set.mul_indicator_comp_of_one Set.mulIndicator_comp_of_one
#align set.indicator_comp_of_zero Set.indicator_comp_of_zero

@[to_additive]
theorem comp_mulIndicator_const (c : M) (f : M â†’ N) (hf : f 1 = 1) :
    (fun x => f (s.mulIndicator (fun _ => c) x)) = s.mulIndicator fun _ => f c :=
  (mulIndicator_comp_of_one hf).symm
#align set.comp_mul_indicator_const Set.comp_mulIndicator_const
#align set.comp_indicator_const Set.comp_indicator_const

@[to_additive]
theorem mulIndicator_preimage (s : Set Î±) (f : Î± â†’ M) (B : Set M) :
    mulIndicator s f â»Â¹' B = s.ite (f â»Â¹' B) (1 â»Â¹' B) :=
  letI := Classical.decPred (Â· âˆˆ s)
  piecewise_preimage s f 1 B
#align set.mul_indicator_preimage Set.mulIndicator_preimage
#align set.indicator_preimage Set.indicator_preimage

@[to_additive]
theorem mulIndicator_one_preimage (s : Set M) :
    t.mulIndicator 1 â»Â¹' s âˆˆ ({Set.univ, âˆ…} : Set (Set Î±)) := by
  classical
    rw [mulIndicator_one', preimage_one]
    split_ifs <;> simp
#align set.mul_indicator_one_preimage Set.mulIndicator_one_preimage
#align set.indicator_zero_preimage Set.indicator_zero_preimage

@[to_additive]
theorem mulIndicator_const_preimage_eq_union (U : Set Î±) (s : Set M) (a : M) [Decidable (a âˆˆ s)]
    [Decidable ((1 : M) âˆˆ s)] : (U.mulIndicator fun _ => a) â»Â¹' s =
      (if a âˆˆ s then U else âˆ…) âˆª if (1 : M) âˆˆ s then Uá¶œ else âˆ… := by
  rw [mulIndicator_preimage, preimage_one, preimage_const]
  -- âŠ¢ Set.ite U (if a âˆˆ s then univ else âˆ…) (if 1 âˆˆ s then univ else âˆ…) = (if a âˆˆ  â€¦
  split_ifs <;> simp [â† compl_eq_univ_diff]
                -- ğŸ‰ no goals
                -- ğŸ‰ no goals
                -- ğŸ‰ no goals
                -- ğŸ‰ no goals
#align set.mul_indicator_const_preimage_eq_union Set.mulIndicator_const_preimage_eq_union
#align set.indicator_const_preimage_eq_union Set.indicator_const_preimage_eq_union

@[to_additive]
theorem mulIndicator_const_preimage (U : Set Î±) (s : Set M) (a : M) :
    (U.mulIndicator fun _ => a) â»Â¹' s âˆˆ ({Set.univ, U, Uá¶œ, âˆ…} : Set (Set Î±)) := by
  classical
    rw [mulIndicator_const_preimage_eq_union]
    split_ifs <;> simp
#align set.mul_indicator_const_preimage Set.mulIndicator_const_preimage
#align set.indicator_const_preimage Set.indicator_const_preimage

theorem indicator_one_preimage [Zero M] (U : Set Î±) (s : Set M) :
    U.indicator 1 â»Â¹' s âˆˆ ({Set.univ, U, Uá¶œ, âˆ…} : Set (Set Î±)) :=
  indicator_const_preimage _ _ 1
#align set.indicator_one_preimage Set.indicator_one_preimage

@[to_additive]
theorem mulIndicator_preimage_of_not_mem (s : Set Î±) (f : Î± â†’ M) {t : Set M} (ht : (1 : M) âˆ‰ t) :
    mulIndicator s f â»Â¹' t = f â»Â¹' t âˆ© s := by
  simp [mulIndicator_preimage, Pi.one_def, Set.preimage_const_of_not_mem ht]
  -- ğŸ‰ no goals
#align set.mul_indicator_preimage_of_not_mem Set.mulIndicator_preimage_of_not_mem
#align set.indicator_preimage_of_not_mem Set.indicator_preimage_of_not_mem

@[to_additive]
theorem mem_range_mulIndicator {r : M} {s : Set Î±} {f : Î± â†’ M} :
    r âˆˆ range (mulIndicator s f) â†” r = 1 âˆ§ s â‰  univ âˆ¨ r âˆˆ f '' s := by
-- Porting note: This proof used to be:
  -- simp [mulIndicator, ite_eq_iff, exists_or, eq_univ_iff_forall, and_comm, or_comm,
  -- @eq_comm _ r 1]
  simp only [mem_range, mulIndicator, ne_eq, mem_image]
  -- âŠ¢ (âˆƒ y, (if y âˆˆ s then f y else 1) = r) â†” r = 1 âˆ§ Â¬s = univ âˆ¨ âˆƒ x, x âˆˆ s âˆ§ f x â€¦
  rw [eq_univ_iff_forall, not_forall]
  -- âŠ¢ (âˆƒ y, (if y âˆˆ s then f y else 1) = r) â†” (r = 1 âˆ§ âˆƒ x, Â¬x âˆˆ s) âˆ¨ âˆƒ x, x âˆˆ s âˆ§ â€¦
  refine âŸ¨?_, ?_âŸ©
  -- âŠ¢ (âˆƒ y, (if y âˆˆ s then f y else 1) = r) â†’ (r = 1 âˆ§ âˆƒ x, Â¬x âˆˆ s) âˆ¨ âˆƒ x, x âˆˆ s âˆ§ â€¦
  Â· rintro âŸ¨y, hyâŸ©
    -- âŠ¢ (r = 1 âˆ§ âˆƒ x, Â¬x âˆˆ s) âˆ¨ âˆƒ x, x âˆˆ s âˆ§ f x = r
    split_ifs at hy with hys
    -- âŠ¢ (r = 1 âˆ§ âˆƒ x, Â¬x âˆˆ s) âˆ¨ âˆƒ x, x âˆˆ s âˆ§ f x = r
    Â· tauto
      -- ğŸ‰ no goals
    Â· left
      -- âŠ¢ r = 1 âˆ§ âˆƒ x, Â¬x âˆˆ s
      tauto
      -- ğŸ‰ no goals
  Â· rintro (âŸ¨hr, âŸ¨x, hxâŸ©âŸ© | âŸ¨x, âŸ¨hx, hxsâŸ©âŸ©) <;> use x <;> split_ifs <;> tauto
    -- âŠ¢ âˆƒ y, (if y âˆˆ s then f y else 1) = r
                                                -- âŠ¢ (if x âˆˆ s then f x else 1) = r
                                                -- âŠ¢ (if x âˆˆ s then f x else 1) = r
                                                          -- âŠ¢ 1 = r
                                                          -- âŠ¢ f x = r
                                                                        -- ğŸ‰ no goals
                                                                        -- ğŸ‰ no goals
#align set.mem_range_mul_indicator Set.mem_range_mulIndicator
#align set.mem_range_indicator Set.mem_range_indicator

@[to_additive]
theorem mulIndicator_rel_mulIndicator {r : M â†’ M â†’ Prop} (h1 : r 1 1) (ha : a âˆˆ s â†’ r (f a) (g a)) :
    r (mulIndicator s f a) (mulIndicator s g a) := by
  simp only [mulIndicator]
  -- âŠ¢ r (if a âˆˆ s then f a else 1) (if a âˆˆ s then g a else 1)
  split_ifs with has
  -- âŠ¢ r (f a) (g a)
  exacts [ha has, h1]
  -- ğŸ‰ no goals
#align set.mul_indicator_rel_mul_indicator Set.mulIndicator_rel_mulIndicator
#align set.indicator_rel_indicator Set.indicator_rel_indicator

end One

section Monoid

variable [MulOneClass M] {s t : Set Î±} {f g : Î± â†’ M} {a : Î±}

@[to_additive]
theorem mulIndicator_union_mul_inter_apply (f : Î± â†’ M) (s t : Set Î±) (a : Î±) :
    mulIndicator (s âˆª t) f a * mulIndicator (s âˆ© t) f a = mulIndicator s f a * mulIndicator t f a :=
  by by_cases hs : a âˆˆ s <;> by_cases ht : a âˆˆ t <;> simp [*]
     -- âŠ¢ mulIndicator (s âˆª t) f a * mulIndicator (s âˆ© t) f a = mulIndicator s f a * m â€¦
                             -- âŠ¢ mulIndicator (s âˆª t) f a * mulIndicator (s âˆ© t) f a = mulIndicator s f a * m â€¦
                             -- âŠ¢ mulIndicator (s âˆª t) f a * mulIndicator (s âˆ© t) f a = mulIndicator s f a * m â€¦
                                                     -- ğŸ‰ no goals
                                                     -- ğŸ‰ no goals
                                                     -- ğŸ‰ no goals
                                                     -- ğŸ‰ no goals
#align set.mul_indicator_union_mul_inter_apply Set.mulIndicator_union_mul_inter_apply
#align set.indicator_union_add_inter_apply Set.indicator_union_add_inter_apply

@[to_additive]
theorem mulIndicator_union_mul_inter (f : Î± â†’ M) (s t : Set Î±) :
    mulIndicator (s âˆª t) f * mulIndicator (s âˆ© t) f = mulIndicator s f * mulIndicator t f :=
  funext <| mulIndicator_union_mul_inter_apply f s t
#align set.mul_indicator_union_mul_inter Set.mulIndicator_union_mul_inter
#align set.indicator_union_add_inter Set.indicator_union_add_inter

@[to_additive]
theorem mulIndicator_union_of_not_mem_inter (h : a âˆ‰ s âˆ© t) (f : Î± â†’ M) :
    mulIndicator (s âˆª t) f a = mulIndicator s f a * mulIndicator t f a := by
  rw [â† mulIndicator_union_mul_inter_apply f s t, mulIndicator_of_not_mem h, mul_one]
  -- ğŸ‰ no goals
#align set.mul_indicator_union_of_not_mem_inter Set.mulIndicator_union_of_not_mem_inter
#align set.indicator_union_of_not_mem_inter Set.indicator_union_of_not_mem_inter

@[to_additive]
theorem mulIndicator_union_of_disjoint (h : Disjoint s t) (f : Î± â†’ M) :
    mulIndicator (s âˆª t) f = fun a => mulIndicator s f a * mulIndicator t f a :=
  funext fun _ => mulIndicator_union_of_not_mem_inter (fun ha => h.le_bot ha) _
#align set.mul_indicator_union_of_disjoint Set.mulIndicator_union_of_disjoint
#align set.indicator_union_of_disjoint Set.indicator_union_of_disjoint

@[to_additive]
theorem mulIndicator_symmDiff (s t : Set Î±) (f : Î± â†’ M) :
    mulIndicator (s âˆ† t) f = mulIndicator (s \ t) f * mulIndicator (t \ s) f :=
  mulIndicator_union_of_disjoint (disjoint_sdiff_self_right.mono_left sdiff_le) _

@[to_additive]
theorem mulIndicator_mul (s : Set Î±) (f g : Î± â†’ M) :
    (mulIndicator s fun a => f a * g a) = fun a => mulIndicator s f a * mulIndicator s g a := by
  funext
  -- âŠ¢ mulIndicator s (fun a => f a * g a) xâœ = mulIndicator s f xâœ * mulIndicator  â€¦
  simp only [mulIndicator]
  -- âŠ¢ (if xâœ âˆˆ s then f xâœ * g xâœ else 1) = (if xâœ âˆˆ s then f xâœ else 1) * if xâœ âˆˆ â€¦
  split_ifs
  -- âŠ¢ f xâœ * g xâœ = f xâœ * g xâœ
  Â· rfl
    -- ğŸ‰ no goals
  rw [mul_one]
  -- ğŸ‰ no goals
#align set.mul_indicator_mul Set.mulIndicator_mul
#align set.indicator_add Set.indicator_add

@[to_additive]
theorem mulIndicator_mul' (s : Set Î±) (f g : Î± â†’ M) :
    mulIndicator s (f * g) = mulIndicator s f * mulIndicator s g :=
  mulIndicator_mul s f g
#align set.mul_indicator_mul' Set.mulIndicator_mul'
#align set.indicator_add' Set.indicator_add'

@[to_additive (attr := simp)]
theorem mulIndicator_compl_mul_self_apply (s : Set Î±) (f : Î± â†’ M) (a : Î±) :
    mulIndicator sá¶œ f a * mulIndicator s f a = f a :=
  by_cases (fun ha : a âˆˆ s => by simp [ha]) fun ha => by simp [ha]
                                 -- ğŸ‰ no goals
                                                         -- ğŸ‰ no goals
#align set.mul_indicator_compl_mul_self_apply Set.mulIndicator_compl_mul_self_apply
#align set.indicator_compl_add_self_apply Set.indicator_compl_add_self_apply

@[to_additive (attr := simp)]
theorem mulIndicator_compl_mul_self (s : Set Î±) (f : Î± â†’ M) :
    mulIndicator sá¶œ f * mulIndicator s f = f :=
  funext <| mulIndicator_compl_mul_self_apply s f
#align set.mul_indicator_compl_mul_self Set.mulIndicator_compl_mul_self
#align set.indicator_compl_add_self Set.indicator_compl_add_self

@[to_additive (attr := simp)]
theorem mulIndicator_self_mul_compl_apply (s : Set Î±) (f : Î± â†’ M) (a : Î±) :
    mulIndicator s f a * mulIndicator sá¶œ f a = f a :=
  by_cases (fun ha : a âˆˆ s => by simp [ha]) fun ha => by simp [ha]
                                 -- ğŸ‰ no goals
                                                         -- ğŸ‰ no goals
#align set.mul_indicator_self_mul_compl_apply Set.mulIndicator_self_mul_compl_apply
#align set.indicator_self_add_compl_apply Set.indicator_self_add_compl_apply

@[to_additive (attr := simp)]
theorem mulIndicator_self_mul_compl (s : Set Î±) (f : Î± â†’ M) :
    mulIndicator s f * mulIndicator sá¶œ f = f :=
  funext <| mulIndicator_self_mul_compl_apply s f
#align set.mul_indicator_self_mul_compl Set.mulIndicator_self_mul_compl
#align set.indicator_self_add_compl Set.indicator_self_add_compl

@[to_additive]
theorem mulIndicator_mul_eq_left {f g : Î± â†’ M} (h : Disjoint (mulSupport f) (mulSupport g)) :
    (mulSupport f).mulIndicator (f * g) = f := by
  refine' (mulIndicator_congr fun x hx => _).trans mulIndicator_mulSupport
  -- âŠ¢ (f * g) x = f x
  have : g x = 1 := nmem_mulSupport.1 (disjoint_left.1 h hx)
  -- âŠ¢ (f * g) x = f x
  rw [Pi.mul_apply, this, mul_one]
  -- ğŸ‰ no goals
#align set.mul_indicator_mul_eq_left Set.mulIndicator_mul_eq_left
#align set.indicator_add_eq_left Set.indicator_add_eq_left

@[to_additive]
theorem mulIndicator_mul_eq_right {f g : Î± â†’ M} (h : Disjoint (mulSupport f) (mulSupport g)) :
    (mulSupport g).mulIndicator (f * g) = g := by
  refine' (mulIndicator_congr fun x hx => _).trans mulIndicator_mulSupport
  -- âŠ¢ (f * g) x = g x
  have : f x = 1 := nmem_mulSupport.1 (disjoint_right.1 h hx)
  -- âŠ¢ (f * g) x = g x
  rw [Pi.mul_apply, this, one_mul]
  -- ğŸ‰ no goals
#align set.mul_indicator_mul_eq_right Set.mulIndicator_mul_eq_right
#align set.indicator_add_eq_right Set.indicator_add_eq_right

@[to_additive]
theorem mulIndicator_mul_compl_eq_piecewise [DecidablePred (Â· âˆˆ s)] (f g : Î± â†’ M) :
    s.mulIndicator f * sá¶œ.mulIndicator g = s.piecewise f g := by
  ext x
  -- âŠ¢ (mulIndicator s f * mulIndicator sá¶œ g) x = piecewise s f g x
  by_cases h : x âˆˆ s
  -- âŠ¢ (mulIndicator s f * mulIndicator sá¶œ g) x = piecewise s f g x
  Â· rw [piecewise_eq_of_mem _ _ _ h, Pi.mul_apply, Set.mulIndicator_of_mem h,
      Set.mulIndicator_of_not_mem (Set.not_mem_compl_iff.2 h), mul_one]
  Â· rw [piecewise_eq_of_not_mem _ _ _ h, Pi.mul_apply, Set.mulIndicator_of_not_mem h,
      Set.mulIndicator_of_mem (Set.mem_compl h), one_mul]
#align set.mul_indicator_mul_compl_eq_piecewise Set.mulIndicator_mul_compl_eq_piecewise
#align set.indicator_add_compl_eq_piecewise Set.indicator_add_compl_eq_piecewise

/-- `Set.mulIndicator` as a `monoidHom`. -/
@[to_additive "`Set.indicator` as an `addMonoidHom`."]
noncomputable def mulIndicatorHom {Î±} (M) [MulOneClass M] (s : Set Î±) : (Î± â†’ M) â†’* Î± â†’ M
    where
  toFun := mulIndicator s
  map_one' := mulIndicator_one M s
  map_mul' := mulIndicator_mul s
#align set.mul_indicator_hom Set.mulIndicatorHom
#align set.indicator_hom Set.indicatorHom

end Monoid

section DistribMulAction

variable {A : Type*} [AddMonoid A] [Monoid M] [DistribMulAction M A]

theorem indicator_smul_apply (s : Set Î±) (r : Î± â†’ M) (f : Î± â†’ A) (x : Î±) :
    indicator s (fun x => r x â€¢ f x) x = r x â€¢ indicator s f x := by
  dsimp only [indicator]
  -- âŠ¢ (if x âˆˆ s then r x â€¢ f x else 0) = r x â€¢ if x âˆˆ s then f x else 0
  split_ifs
  -- âŠ¢ r x â€¢ f x = r x â€¢ f x
  exacts [rfl, (smul_zero (r x)).symm]
  -- ğŸ‰ no goals
#align set.indicator_smul_apply Set.indicator_smul_apply

theorem indicator_smul (s : Set Î±) (r : Î± â†’ M) (f : Î± â†’ A) :
    (indicator s fun x : Î± => r x â€¢ f x) = fun x : Î± => r x â€¢ indicator s f x :=
  funext <| indicator_smul_apply s r f
#align set.indicator_smul Set.indicator_smul

theorem indicator_const_smul_apply (s : Set Î±) (r : M) (f : Î± â†’ A) (x : Î±) :
    indicator s (fun x => r â€¢ f x) x = r â€¢ indicator s f x :=
  indicator_smul_apply s (fun _ => r) f x
#align set.indicator_const_smul_apply Set.indicator_const_smul_apply

theorem indicator_const_smul (s : Set Î±) (r : M) (f : Î± â†’ A) :
    (indicator s fun x : Î± => r â€¢ f x) = fun x : Î± => r â€¢ indicator s f x :=
  funext <| indicator_const_smul_apply s r f
#align set.indicator_const_smul Set.indicator_const_smul

end DistribMulAction

section SMulWithZero

variable {A : Type*} [Zero A] [Zero M] [SMulWithZero M A]

theorem indicator_smul_apply_left (s : Set Î±) (r : Î± â†’ M) (f : Î± â†’ A) (x : Î±) :
    indicator s (fun x => r x â€¢ f x) x = indicator s r x â€¢ f x := by
  dsimp only [indicator]
  -- âŠ¢ (if x âˆˆ s then r x â€¢ f x else 0) = (if x âˆˆ s then r x else 0) â€¢ f x
  split_ifs
  -- âŠ¢ r x â€¢ f x = r x â€¢ f x
  exacts [rfl, (zero_smul _ (f x)).symm]
  -- ğŸ‰ no goals

theorem indicator_smul_left (s : Set Î±) (r : Î± â†’ M) (f : Î± â†’ A) :
    (indicator s fun x : Î± => r x â€¢ f x) = fun x : Î± => indicator s r x â€¢ f x :=
  funext <| indicator_smul_apply_left s r f

theorem indicator_smul_const_apply (s : Set Î±) (r : Î± â†’ M) (a : A) (x : Î±) :
    indicator s (fun x => r x â€¢ a) x = indicator s r x â€¢ a :=
  indicator_smul_apply_left s r (fun _ => a) x

theorem indicator_smul_const (s : Set Î±) (r : Î± â†’ M) (a : A) :
    (indicator s fun x : Î± => r x â€¢ a) = fun x : Î± => indicator s r x â€¢ a :=
  funext <| indicator_smul_const_apply s r a

end SMulWithZero

section Group

variable {G : Type*} [Group G] {s t : Set Î±} {f g : Î± â†’ G} {a : Î±}

@[to_additive]
theorem mulIndicator_inv' (s : Set Î±) (f : Î± â†’ G) : mulIndicator s fâ»Â¹ = (mulIndicator s f)â»Â¹ :=
  (mulIndicatorHom G s).map_inv f
#align set.mul_indicator_inv' Set.mulIndicator_inv'
#align set.indicator_neg' Set.indicator_neg'

@[to_additive]
theorem mulIndicator_inv (s : Set Î±) (f : Î± â†’ G) :
    (mulIndicator s fun a => (f a)â»Â¹) = fun a => (mulIndicator s f a)â»Â¹ :=
  mulIndicator_inv' s f
#align set.mul_indicator_inv Set.mulIndicator_inv
#align set.indicator_neg Set.indicator_neg

@[to_additive]
theorem mulIndicator_div (s : Set Î±) (f g : Î± â†’ G) :
    (mulIndicator s fun a => f a / g a) = fun a => mulIndicator s f a / mulIndicator s g a :=
  (mulIndicatorHom G s).map_div f g
#align set.mul_indicator_div Set.mulIndicator_div
#align set.indicator_sub Set.indicator_sub

@[to_additive]
theorem mulIndicator_div' (s : Set Î±) (f g : Î± â†’ G) :
    mulIndicator s (f / g) = mulIndicator s f / mulIndicator s g :=
  mulIndicator_div s f g
#align set.mul_indicator_div' Set.mulIndicator_div'
#align set.indicator_sub' Set.indicator_sub'

@[to_additive indicator_compl']
theorem mulIndicator_compl (s : Set Î±) (f : Î± â†’ G) :
    mulIndicator sá¶œ f = f * (mulIndicator s f)â»Â¹ :=
  eq_mul_inv_of_mul_eq <| s.mulIndicator_compl_mul_self f
#align set.mul_indicator_compl Set.mulIndicator_compl
#align set.indicator_compl' Set.indicator_compl'

@[to_additive indicator_compl]
theorem mulIndicator_compl' (s : Set Î±) (f : Î± â†’ G) :
    mulIndicator sá¶œ f = f / mulIndicator s f := by rw [div_eq_mul_inv, mulIndicator_compl]
                                                   -- ğŸ‰ no goals
#align set.indicator_compl Set.indicator_compl

@[to_additive indicator_diff']
theorem mulIndicator_diff (h : s âŠ† t) (f : Î± â†’ G) :
    mulIndicator (t \ s) f = mulIndicator t f * (mulIndicator s f)â»Â¹ :=
  eq_mul_inv_of_mul_eq <| by
    rw [Pi.mul_def, â† mulIndicator_union_of_disjoint, diff_union_self,
      union_eq_self_of_subset_right h]
    exact disjoint_sdiff_self_left
    -- ğŸ‰ no goals
#align set.mul_indicator_diff Set.mulIndicator_diff
#align set.indicator_diff' Set.indicator_diff'

@[to_additive indicator_diff]
theorem mulIndicator_diff' (h : s âŠ† t) (f : Î± â†’ G) :
    mulIndicator (t \ s) f = mulIndicator t f / mulIndicator s f := by
  rw [mulIndicator_diff h, div_eq_mul_inv]
  -- ğŸ‰ no goals
#align set.indicator_diff Set.indicator_diff

@[to_additive]
theorem apply_mulIndicator_symmDiff {g : G â†’ Î²} (hg : âˆ€ x, g xâ»Â¹ = g x)
    (s t : Set Î±) (f : Î± â†’ G) (x : Î±):
    g (mulIndicator (s âˆ† t) f x) = g (mulIndicator s f x / mulIndicator t f x) := by
  by_cases hs : x âˆˆ s <;> by_cases ht : x âˆˆ t <;> simp [mem_symmDiff, *]
  -- âŠ¢ g (mulIndicator (s âˆ† t) f x) = g (mulIndicator s f x / mulIndicator t f x)
                          -- âŠ¢ g (mulIndicator (s âˆ† t) f x) = g (mulIndicator s f x / mulIndicator t f x)
                          -- âŠ¢ g (mulIndicator (s âˆ† t) f x) = g (mulIndicator s f x / mulIndicator t f x)
                                                  -- ğŸ‰ no goals
                                                  -- ğŸ‰ no goals
                                                  -- ğŸ‰ no goals
                                                  -- ğŸ‰ no goals

end Group

theorem abs_indicator_symmDiff {G : Type*} [LinearOrderedAddCommGroup G]
    (s t : Set Î±) (f : Î± â†’ G) (x : Î±) :
    |indicator (s âˆ† t) f x| = |indicator s f x - indicator t f x| :=
  apply_indicator_symmDiff abs_neg s t f x

section CommMonoid

variable [CommMonoid M]

/-- Consider a product of `g i (f i)` over a `Finset`.  Suppose `g` is a
function such as `Pow`, which maps a second argument of `1` to
`1`. Then if `f` is replaced by the corresponding multiplicative indicator
function, the `Finset` may be replaced by a possibly larger `Finset`
without changing the value of the sum. -/
@[to_additive]
theorem prod_mulIndicator_subset_of_eq_one [One N] (f : Î± â†’ N) (g : Î± â†’ N â†’ M) {s t : Finset Î±}
    (h : s âŠ† t) (hg : âˆ€ a, g a 1 = 1) :
    (âˆ i in s, g i (f i)) = âˆ i in t, g i (mulIndicator (â†‘s) f i) := by
  rw [â† Finset.prod_subset h _]
  -- âŠ¢ âˆ i in s, g i (f i) = âˆ x in s, g x (mulIndicator (â†‘s) f x)
  Â· apply Finset.prod_congr rfl
    -- âŠ¢ âˆ€ (x : Î±), x âˆˆ s â†’ g x (f x) = g x (mulIndicator (â†‘s) f x)
    intro i hi
    -- âŠ¢ g i (f i) = g i (mulIndicator (â†‘s) f i)
    congr
    -- âŠ¢ f i = mulIndicator (â†‘s) f i
    symm
    -- âŠ¢ mulIndicator (â†‘s) f i = f i
    -- Porting note: This did not use to need the implicit argument
    exact mulIndicator_of_mem (Î± := Î±) hi f
    -- ğŸ‰ no goals
  Â· refine' fun i _ hn => _
    -- âŠ¢ g i (mulIndicator (â†‘s) f i) = 1
    convert hg i
    -- âŠ¢ mulIndicator (â†‘s) f i = 1
    -- Porting note: This did not use to need the implicit argument
    exact mulIndicator_of_not_mem (Î± := Î±) hn f
    -- ğŸ‰ no goals
#align set.prod_mul_indicator_subset_of_eq_one Set.prod_mulIndicator_subset_of_eq_one
#align set.sum_indicator_subset_of_eq_zero Set.sum_indicator_subset_of_eq_zero

/-- Consider a sum of `g i (f i)` over a `Finset`. Suppose `g` is a
function such as multiplication, which maps a second argument of 0 to
0.  (A typical use case would be a weighted sum of `f i * h i` or `f i â€¢ h i`,
where `f` gives the weights that are multiplied by some other
function `h`.)  Then if `f` is replaced by the corresponding indicator
function, the `Finset` may be replaced by a possibly larger `Finset`
without changing the value of the sum. -/
add_decl_doc Set.sum_indicator_subset_of_eq_zero

/-- Taking the product of an indicator function over a possibly larger `Finset` is the same as
taking the original function over the original `Finset`. -/
@[to_additive
      "Summing an indicator function over a possibly larger `Finset` is the same as summing
      the original function over the original `finset`."]
theorem prod_mulIndicator_subset (f : Î± â†’ M) {s t : Finset Î±} (h : s âŠ† t) :
    âˆ i in s, f i = âˆ i in t, mulIndicator (â†‘s) f i :=
  prod_mulIndicator_subset_of_eq_one _ (fun _ b => b) h fun _ => rfl
#align set.prod_mul_indicator_subset Set.prod_mulIndicator_subset
#align set.sum_indicator_subset Set.sum_indicator_subset

@[to_additive]
theorem _root_.Finset.prod_mulIndicator_eq_prod_filter (s : Finset Î¹) (f : Î¹ â†’ Î± â†’ M)
    (t : Î¹ â†’ Set Î±) (g : Î¹ â†’ Î±) [DecidablePred fun i => g i âˆˆ t i] :
    (âˆ i in s, mulIndicator (t i) (f i) (g i)) = âˆ i in s.filter fun i => g i âˆˆ t i, f i (g i) := by
  refine' (Finset.prod_filter_mul_prod_filter_not s (fun i => g i âˆˆ t i) _).symm.trans _
  -- âŠ¢ (âˆ x in Finset.filter (fun i => g i âˆˆ t i) s, mulIndicator (t x) (f x) (g x) â€¦
  refine' Eq.trans _ (mul_one _)
  -- âŠ¢ (âˆ x in Finset.filter (fun i => g i âˆˆ t i) s, mulIndicator (t x) (f x) (g x) â€¦
  exact
    congr_argâ‚‚ (Â· * Â·)
      (Finset.prod_congr rfl fun x hx => mulIndicator_of_mem (Finset.mem_filter.1 hx).2 _)
      (Finset.prod_eq_one fun x hx => mulIndicator_of_not_mem (Finset.mem_filter.1 hx).2 _)
#align finset.prod_mul_indicator_eq_prod_filter Finset.prod_mulIndicator_eq_prod_filter
#align finset.sum_indicator_eq_sum_filter Finset.sum_indicator_eq_sum_filter

@[to_additive]
theorem mulIndicator_finset_prod (I : Finset Î¹) (s : Set Î±) (f : Î¹ â†’ Î± â†’ M) :
    mulIndicator s (âˆ i in I, f i) = âˆ i in I, mulIndicator s (f i) :=
  (mulIndicatorHom M s).map_prod _ _
#align set.mul_indicator_finset_prod Set.mulIndicator_finset_prod
#align set.indicator_finset_sum Set.indicator_finset_sum

@[to_additive]
theorem mulIndicator_finset_biUnion (I : Finset Î¹) (s : Î¹ â†’ Set Î±) {f : Î± â†’ M} :
    (âˆ€ i âˆˆ I, âˆ€ j âˆˆ I, i â‰  j â†’ Disjoint (s i) (s j)) â†’
      mulIndicator (â‹ƒ i âˆˆ I, s i) f = fun a => âˆ i in I, mulIndicator (s i) f a := by
  classical
    refine' Finset.induction_on I _ _
    Â· intro
      funext
      simp
    intro a I haI ih hI
    funext
    rw [Finset.prod_insert haI, Finset.set_biUnion_insert, mulIndicator_union_of_not_mem_inter,
      ih _]
    Â· intro i hi j hj hij
      exact hI i (Finset.mem_insert_of_mem hi) j (Finset.mem_insert_of_mem hj) hij
    simp only [not_exists, exists_prop, mem_iUnion, mem_inter_iff, not_and]
    intro hx a' ha'
    refine'
      disjoint_left.1 (hI a (Finset.mem_insert_self _ _) a' (Finset.mem_insert_of_mem ha') _) hx
    exact (ne_of_mem_of_not_mem ha' haI).symm
#align set.mul_indicator_finset_bUnion Set.mulIndicator_finset_biUnion
#align set.indicator_finset_bUnion Set.indicator_finset_biUnion

@[to_additive]
theorem mulIndicator_finset_biUnion_apply (I : Finset Î¹) (s : Î¹ â†’ Set Î±) {f : Î± â†’ M}
    (h : âˆ€ i âˆˆ I, âˆ€ j âˆˆ I, i â‰  j â†’ Disjoint (s i) (s j)) (x : Î±) :
    mulIndicator (â‹ƒ i âˆˆ I, s i) f x = âˆ i in I, mulIndicator (s i) f x := by
  rw [Set.mulIndicator_finset_biUnion I s h]
  -- ğŸ‰ no goals
#align set.mul_indicator_finset_bUnion_apply Set.mulIndicator_finset_biUnion_apply
#align set.indicator_finset_bUnion_apply Set.indicator_finset_biUnion_apply

end CommMonoid

section MulZeroClass

variable [MulZeroClass M] {s t : Set Î±} {f g : Î± â†’ M} {a : Î±}

theorem indicator_mul (s : Set Î±) (f g : Î± â†’ M) :
    (indicator s fun a => f a * g a) = fun a => indicator s f a * indicator s g a := by
  funext
  -- âŠ¢ indicator s (fun a => f a * g a) xâœ = indicator s f xâœ * indicator s g xâœ
  simp only [indicator]
  -- âŠ¢ (if xâœ âˆˆ s then f xâœ * g xâœ else 0) = (if xâœ âˆˆ s then f xâœ else 0) * if xâœ âˆˆ â€¦
  split_ifs
  -- âŠ¢ f xâœ * g xâœ = f xâœ * g xâœ
  Â· rfl
    -- ğŸ‰ no goals
  rw [mul_zero]
  -- ğŸ‰ no goals
#align set.indicator_mul Set.indicator_mul

theorem indicator_mul_left (s : Set Î±) (f g : Î± â†’ M) :
    indicator s (fun a => f a * g a) a = indicator s f a * g a := by
  simp only [indicator]
  -- âŠ¢ (if a âˆˆ s then f a * g a else 0) = (if a âˆˆ s then f a else 0) * g a
  split_ifs
  -- âŠ¢ f a * g a = f a * g a
  Â· rfl
    -- ğŸ‰ no goals
  rw [zero_mul]
  -- ğŸ‰ no goals
#align set.indicator_mul_left Set.indicator_mul_left

theorem indicator_mul_right (s : Set Î±) (f g : Î± â†’ M) :
    indicator s (fun a => f a * g a) a = f a * indicator s g a := by
  simp only [indicator]
  -- âŠ¢ (if a âˆˆ s then f a * g a else 0) = f a * if a âˆˆ s then g a else 0
  split_ifs
  -- âŠ¢ f a * g a = f a * g a
  Â· rfl
    -- ğŸ‰ no goals
  rw [mul_zero]
  -- ğŸ‰ no goals
#align set.indicator_mul_right Set.indicator_mul_right

theorem inter_indicator_mul {t1 t2 : Set Î±} (f g : Î± â†’ M) (x : Î±) :
    (t1 âˆ© t2).indicator (fun x => f x * g x) x = t1.indicator f x * t2.indicator g x := by
  rw [â† Set.indicator_indicator]
  -- âŠ¢ indicator t1 (indicator t2 fun x => f x * g x) x = indicator t1 f x * indica â€¦
  simp_rw [indicator]
  -- âŠ¢ (if x âˆˆ t1 then if x âˆˆ t2 then f x * g x else 0 else 0) = (if x âˆˆ t1 then f  â€¦
  split_ifs <;> simp
                -- ğŸ‰ no goals
                -- ğŸ‰ no goals
                -- ğŸ‰ no goals
                -- ğŸ‰ no goals
#align set.inter_indicator_mul Set.inter_indicator_mul

end MulZeroClass

section MulZeroOneClass

variable [MulZeroOneClass M]

theorem inter_indicator_one {s t : Set Î±} :
    (s âˆ© t).indicator (1 : Î± â†’ M) = s.indicator 1 * t.indicator 1 :=
  funext fun _ => by
    simp only [â† inter_indicator_mul, Pi.mul_apply, Pi.one_apply, one_mul]
    -- âŠ¢ indicator (s âˆ© t) 1 xâœ = indicator (s âˆ© t) (fun x => 1) xâœ
    congr
    -- ğŸ‰ no goals
#align set.inter_indicator_one Set.inter_indicator_one

-- Porting note: Think the following note was due to writing (1 : _ â†’ M) instead of (1 : Î± Ã— Î² â†’ M)
/- ./././Mathport/Syntax/Translate/Expr.lean:177:8: unsupported: ambiguous notation -/
theorem indicator_prod_one {s : Set Î±} {t : Set Î²} {x : Î±} {y : Î²} :
    (s Ã—Ë¢ t).indicator (1 : Î± Ã— Î² â†’ M) (x, y) = s.indicator 1 x * t.indicator 1 y := by
  simp_rw [indicator, mem_prod_eq]
  -- âŠ¢ (if x âˆˆ s âˆ§ y âˆˆ t then OfNat.ofNat 1 (x, y) else 0) = (if x âˆˆ s then OfNat.o â€¦
  split_ifs with hâ‚€ <;> simp only [Pi.one_apply, mul_one, mul_zero] <;> tauto
                        -- ğŸ‰ no goals
                        -- âŠ¢ 1 = 0
                        -- âŠ¢ 1 = 0
                        -- âŠ¢ 1 = 0
                        -- âŠ¢ 0 = 1
                        -- ğŸ‰ no goals
                        -- ğŸ‰ no goals
                        -- ğŸ‰ no goals
                                                                        -- ğŸ‰ no goals
                                                                        -- ğŸ‰ no goals
                                                                        -- ğŸ‰ no goals
                                                                        -- ğŸ‰ no goals
#align set.indicator_prod_one Set.indicator_prod_one

variable (M) [Nontrivial M]

theorem indicator_eq_zero_iff_not_mem {U : Set Î±} {x : Î±} : indicator U 1 x = (0 : M) â†” x âˆ‰ U := by
  classical simp [indicator_apply, imp_false]
  -- ğŸ‰ no goals
#align set.indicator_eq_zero_iff_not_mem Set.indicator_eq_zero_iff_not_mem

theorem indicator_eq_one_iff_mem {U : Set Î±} {x : Î±} : indicator U 1 x = (1 : M) â†” x âˆˆ U := by
  classical simp [indicator_apply, imp_false]
  -- ğŸ‰ no goals
#align set.indicator_eq_one_iff_mem Set.indicator_eq_one_iff_mem

theorem indicator_one_inj {U V : Set Î±} (h : indicator U (1 : Î± â†’ M) = indicator V 1) : U = V := by
  ext
  -- âŠ¢ xâœ âˆˆ U â†” xâœ âˆˆ V
  simp_rw [â† indicator_eq_one_iff_mem M, h]
  -- ğŸ‰ no goals
#align set.indicator_one_inj Set.indicator_one_inj

end MulZeroOneClass

section Order

variable [One M] {s t : Set Î±} {f g : Î± â†’ M} {a : Î±} {y : M}

section

variable [LE M]

@[to_additive]
theorem mulIndicator_apply_le' (hfg : a âˆˆ s â†’ f a â‰¤ y) (hg : a âˆ‰ s â†’ 1 â‰¤ y) :
    mulIndicator s f a â‰¤ y := by
  by_cases ha : a âˆˆ s
  -- âŠ¢ mulIndicator s f a â‰¤ y
  Â· simpa [ha] using hfg ha
    -- ğŸ‰ no goals
  Â· simpa [ha] using hg ha
    -- ğŸ‰ no goals
#align set.mul_indicator_apply_le' Set.mulIndicator_apply_le'
#align set.indicator_apply_le' Set.indicator_apply_le'

/- ./././Mathport/Syntax/Translate/Basic.lean:632:2:
warning: expanding binder collection (a Â«expr âˆ‰ Â» s) -/
@[to_additive]
theorem mulIndicator_le' (hfg : âˆ€ a âˆˆ s, f a â‰¤ g a) (hg : âˆ€ (a) (_ : a âˆ‰ s), 1 â‰¤ g a) :
    mulIndicator s f â‰¤ g := fun _ => mulIndicator_apply_le' (hfg _) (hg _)
#align set.mul_indicator_le' Set.mulIndicator_le'
#align set.indicator_le' Set.indicator_le'

@[to_additive]
theorem le_mulIndicator_apply {y} (hfg : a âˆˆ s â†’ y â‰¤ g a) (hf : a âˆ‰ s â†’ y â‰¤ 1) :
    y â‰¤ mulIndicator s g a :=
  @mulIndicator_apply_le' Î± Máµ’áµˆ â€¹_â€º _ _ _ _ _ hfg hf
#align set.le_mul_indicator_apply Set.le_mulIndicator_apply
#align set.le_indicator_apply Set.le_indicator_apply

@[to_additive]
theorem le_mulIndicator (hfg : âˆ€ a âˆˆ s, f a â‰¤ g a) (hf : âˆ€ (a) (_ : a âˆ‰ s), f a â‰¤ 1) :
    f â‰¤ mulIndicator s g := fun _ => le_mulIndicator_apply (hfg _) (hf _)
#align set.le_mul_indicator Set.le_mulIndicator
#align set.le_indicator Set.le_indicator

end

variable [Preorder M]

@[to_additive indicator_apply_nonneg]
theorem one_le_mulIndicator_apply (h : a âˆˆ s â†’ 1 â‰¤ f a) : 1 â‰¤ mulIndicator s f a :=
  le_mulIndicator_apply h fun _ => le_rfl
#align set.one_le_mul_indicator_apply Set.one_le_mulIndicator_apply
#align set.indicator_apply_nonneg Set.indicator_apply_nonneg

@[to_additive indicator_nonneg]
theorem one_le_mulIndicator (h : âˆ€ a âˆˆ s, 1 â‰¤ f a) (a : Î±) : 1 â‰¤ mulIndicator s f a :=
  one_le_mulIndicator_apply (h a)
#align set.one_le_mul_indicator Set.one_le_mulIndicator
#align set.indicator_nonneg Set.indicator_nonneg

@[to_additive]
theorem mulIndicator_apply_le_one (h : a âˆˆ s â†’ f a â‰¤ 1) : mulIndicator s f a â‰¤ 1 :=
  mulIndicator_apply_le' h fun _ => le_rfl
#align set.mul_indicator_apply_le_one Set.mulIndicator_apply_le_one
#align set.indicator_apply_nonpos Set.indicator_apply_nonpos

@[to_additive]
theorem mulIndicator_le_one (h : âˆ€ a âˆˆ s, f a â‰¤ 1) (a : Î±) : mulIndicator s f a â‰¤ 1 :=
  mulIndicator_apply_le_one (h a)
#align set.mul_indicator_le_one Set.mulIndicator_le_one
#align set.indicator_nonpos Set.indicator_nonpos

@[to_additive]
theorem mulIndicator_le_mulIndicator (h : f a â‰¤ g a) : mulIndicator s f a â‰¤ mulIndicator s g a :=
  mulIndicator_rel_mulIndicator le_rfl fun _ => h
#align set.mul_indicator_le_mul_indicator Set.mulIndicator_le_mulIndicator
#align set.indicator_le_indicator Set.indicator_le_indicator

attribute [mono] mulIndicator_le_mulIndicator indicator_le_indicator

@[to_additive]
theorem mulIndicator_le_mulIndicator_of_subset (h : s âŠ† t) (hf : âˆ€ a, 1 â‰¤ f a) (a : Î±) :
    mulIndicator s f a â‰¤ mulIndicator t f a :=
  mulIndicator_apply_le'
    (fun ha => le_mulIndicator_apply (fun _ => le_rfl) fun hat => (hat <| h ha).elim) fun _ =>
    one_le_mulIndicator_apply fun _ => hf _
#align set.mul_indicator_le_mul_indicator_of_subset Set.mulIndicator_le_mulIndicator_of_subset
#align set.indicator_le_indicator_of_subset Set.indicator_le_indicator_of_subset

@[to_additive]
theorem mulIndicator_le_self' (hf : âˆ€ (x) (_ : x âˆ‰ s), 1 â‰¤ f x) : mulIndicator s f â‰¤ f :=
  mulIndicator_le' (fun _ _ => le_rfl) hf
#align set.mul_indicator_le_self' Set.mulIndicator_le_self'
#align set.indicator_le_self' Set.indicator_le_self'

@[to_additive]
theorem mulIndicator_iUnion_apply {Î¹ : Sort*} {M : Type*} [CompleteLattice M] [One M]
    (h1 : (âŠ¥ : M) = 1) (s : Î¹ â†’ Set Î±) (f : Î± â†’ M) (x : Î±) :
    mulIndicator (â‹ƒ i, s i) f x = â¨† i, mulIndicator (s i) f x := by
  by_cases hx : x âˆˆ â‹ƒ i, s i
  -- âŠ¢ mulIndicator (â‹ƒ (i : Î¹), s i) f x = â¨† (i : Î¹), mulIndicator (s i) f x
  Â· rw [mulIndicator_of_mem hx]
    -- âŠ¢ f x = â¨† (i : Î¹), mulIndicator (s i) f x
    rw [mem_iUnion] at hx
    -- âŠ¢ f x = â¨† (i : Î¹), mulIndicator (s i) f x
    refine' le_antisymm _ (iSup_le fun i => mulIndicator_le_self' (fun x _ => h1 â–¸ bot_le) x)
    -- âŠ¢ f x â‰¤ â¨† (i : Î¹), mulIndicator (s i) f x
    rcases hx with âŸ¨i, hiâŸ©
    -- âŠ¢ f x â‰¤ â¨† (i : Î¹), mulIndicator (s i) f x
    exact le_iSup_of_le i (ge_of_eq <| mulIndicator_of_mem hi _)
    -- ğŸ‰ no goals
  Â· rw [mulIndicator_of_not_mem hx]
    -- âŠ¢ 1 = â¨† (i : Î¹), mulIndicator (s i) f x
    simp only [mem_iUnion, not_exists] at hx
    -- âŠ¢ 1 = â¨† (i : Î¹), mulIndicator (s i) f x
    simp [hx, â† h1]
    -- ğŸ‰ no goals
#align set.mul_indicator_Union_apply Set.mulIndicator_iUnion_apply
#align set.indicator_Union_apply Set.indicator_iUnion_apply

@[to_additive] lemma mulIndicator_iInter_apply {Î¹ : Sort*} {M : Type*} [Nonempty Î¹]
    [CompleteLattice M] [One M] (h1 : (âŠ¥ : M) = 1) (s : Î¹ â†’ Set Î±) (f : Î± â†’ M) (x : Î±) :
    mulIndicator (â‹‚ i, s i) f x = â¨… i, mulIndicator (s i) f x := by
  by_cases hx : x âˆˆ â‹‚ i, s i
  -- âŠ¢ mulIndicator (â‹‚ (i : Î¹), s i) f x = â¨… (i : Î¹), mulIndicator (s i) f x
  Â· rw [mulIndicator_of_mem hx]
    -- âŠ¢ f x = â¨… (i : Î¹), mulIndicator (s i) f x
    rw [mem_iInter] at hx
    -- âŠ¢ f x = â¨… (i : Î¹), mulIndicator (s i) f x
    refine le_antisymm ?_ (by simp only [mulIndicator_of_mem (hx _), ciInf_const, le_refl])
    -- âŠ¢ f x â‰¤ â¨… (i : Î¹), mulIndicator (s i) f x
    exact le_iInf (fun j â†¦ by simp only [mulIndicator_of_mem (hx j), le_refl])
    -- ğŸ‰ no goals
  Â· rw [mulIndicator_of_not_mem hx]
    -- âŠ¢ 1 = â¨… (i : Î¹), mulIndicator (s i) f x
    simp only [mem_iInter, not_exists, not_forall] at hx
    -- âŠ¢ 1 = â¨… (i : Î¹), mulIndicator (s i) f x
    rcases hx with âŸ¨j, hjâŸ©
    -- âŠ¢ 1 = â¨… (i : Î¹), mulIndicator (s i) f x
    refine le_antisymm (by simp only [â† h1, le_iInf_iff, bot_le, forall_const]) ?_
    -- âŠ¢ â¨… (i : Î¹), mulIndicator (s i) f x â‰¤ 1
    simpa [mulIndicator_of_not_mem hj] using (iInf_le (fun i â†¦ (s i).mulIndicator f) j) x
    -- ğŸ‰ no goals

end Order

section CanonicallyOrderedMonoid

variable [CanonicallyOrderedMonoid M]

@[to_additive]
theorem mulIndicator_le_self (s : Set Î±) (f : Î± â†’ M) : mulIndicator s f â‰¤ f :=
  mulIndicator_le_self' fun _ _ => one_le _
#align set.mul_indicator_le_self Set.mulIndicator_le_self
#align set.indicator_le_self Set.indicator_le_self

@[to_additive]
theorem mulIndicator_apply_le {a : Î±} {s : Set Î±} {f g : Î± â†’ M} (hfg : a âˆˆ s â†’ f a â‰¤ g a) :
    mulIndicator s f a â‰¤ g a :=
  mulIndicator_apply_le' hfg fun _ => one_le _
#align set.mul_indicator_apply_le Set.mulIndicator_apply_le
#align set.indicator_apply_le Set.indicator_apply_le

@[to_additive]
theorem mulIndicator_le {s : Set Î±} {f g : Î± â†’ M} (hfg : âˆ€ a âˆˆ s, f a â‰¤ g a) :
    mulIndicator s f â‰¤ g :=
  mulIndicator_le' hfg fun _ _ => one_le _
#align set.mul_indicator_le Set.mulIndicator_le
#align set.indicator_le Set.indicator_le

end CanonicallyOrderedMonoid

theorem indicator_le_indicator_nonneg {Î²} [LinearOrder Î²] [Zero Î²] (s : Set Î±) (f : Î± â†’ Î²) :
    s.indicator f â‰¤ { x | 0 â‰¤ f x }.indicator f := by
  intro x
  -- âŠ¢ indicator s f x â‰¤ indicator {x | 0 â‰¤ f x} f x
  classical
    simp_rw [indicator_apply]
    split_ifs with h_1 h_2 h_3
    Â· exact le_rfl
    Â· exact (not_le.mp h_2).le
    Â· exact h_3
    Â· exact le_rfl
#align set.indicator_le_indicator_nonneg Set.indicator_le_indicator_nonneg

theorem indicator_nonpos_le_indicator {Î²} [LinearOrder Î²] [Zero Î²] (s : Set Î±) (f : Î± â†’ Î²) :
    { x | f x â‰¤ 0 }.indicator f â‰¤ s.indicator f :=
  @indicator_le_indicator_nonneg Î± Î²áµ’áµˆ _ _ s f
#align set.indicator_nonpos_le_indicator Set.indicator_nonpos_le_indicator

end Set

@[to_additive]
theorem MonoidHom.map_mulIndicator {M N : Type*} [MulOneClass M] [MulOneClass N] (f : M â†’* N)
    (s : Set Î±) (g : Î± â†’ M) (x : Î±) : f (s.mulIndicator g x) = s.mulIndicator (f âˆ˜ g) x := by
  simp [Set.mulIndicator_comp_of_one]
  -- ğŸ‰ no goals
#align monoid_hom.map_mul_indicator MonoidHom.map_mulIndicator
#align add_monoid_hom.map_indicator AddMonoidHom.map_indicator
