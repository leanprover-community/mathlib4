/-
Copyright (c) 2022 Michael Stoll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Stoll
-/
import Mathlib.Algebra.CharP.Basic
import Mathlib.GroupTheory.Perm.Cycle.Type

#align_import algebra.char_p.char_and_card from "leanprover-community/mathlib"@"2fae5fd7f90711febdadf19c44dc60fae8834d1b"

/-!
# Characteristic and cardinality

We prove some results relating characteristic and cardinality of finite rings

## Tags
characteristic, cardinality, ring
-/


/-- A prime `p` is a unit in a commutative ring `R` of nonzero characteristic iff it does not divide
the characteristic. -/
theorem isUnit_iff_not_dvd_char_of_ringChar_ne_zero (R : Type*) [CommRing R] (p : â„•) [Fact p.Prime]
    (hR : ringChar R â‰  0) : IsUnit (p : R) â†” Â¬p âˆ£ ringChar R := by
  have hch := CharP.cast_eq_zero R (ringChar R)
  -- âŠ¢ IsUnit â†‘p â†” Â¬p âˆ£ ringChar R
  have hp : p.Prime := Fact.out
  -- âŠ¢ IsUnit â†‘p â†” Â¬p âˆ£ ringChar R
  constructor
  -- âŠ¢ IsUnit â†‘p â†’ Â¬p âˆ£ ringChar R
  Â· rintro hâ‚ âŸ¨q, hqâŸ©
    -- âŠ¢ False
    rcases IsUnit.exists_left_inv hâ‚ with âŸ¨a, haâŸ©
    -- âŠ¢ False
    have hâ‚ƒ : Â¬ringChar R âˆ£ q := by
      rintro âŸ¨r, hrâŸ©
      rw [hr, â† mul_assoc, mul_comm p, mul_assoc] at hq
      nth_rw 1 [â† mul_one (ringChar R)] at hq
      exact Nat.Prime.not_dvd_one hp âŸ¨r, mul_left_cancelâ‚€ hR hqâŸ©
    have hâ‚„ := mt (CharP.int_cast_eq_zero_iff R (ringChar R) q).mp
    -- âŠ¢ False
    apply_fun ((â†‘) : â„• â†’ R) at hq
    -- âŠ¢ False
    apply_fun (Â· * Â·) a at hq
    -- âŠ¢ False
    rw [Nat.cast_mul, hch, mul_zero, â† mul_assoc, ha, one_mul] at hq
    -- âŠ¢ False
    norm_cast at hâ‚„
    -- âŠ¢ False
    exact hâ‚„ hâ‚ƒ hq.symm
    -- ğŸ‰ no goals
  Â· intro h
    -- âŠ¢ IsUnit â†‘p
    rcases(hp.coprime_iff_not_dvd.mpr h).isCoprime with âŸ¨a, b, habâŸ©
    -- âŠ¢ IsUnit â†‘p
    apply_fun ((â†‘) : â„¤ â†’ R) at hab
    -- âŠ¢ IsUnit â†‘p
    push_cast at hab
    -- âŠ¢ IsUnit â†‘p
    rw [hch, mul_zero, add_zero, mul_comm] at hab
    -- âŠ¢ IsUnit â†‘p
    exact isUnit_of_mul_eq_one (p : R) a hab
    -- ğŸ‰ no goals
#align is_unit_iff_not_dvd_char_of_ring_char_ne_zero isUnit_iff_not_dvd_char_of_ringChar_ne_zero

/-- A prime `p` is a unit in a finite commutative ring `R`
iff it does not divide the characteristic. -/
theorem isUnit_iff_not_dvd_char (R : Type*) [CommRing R] (p : â„•) [Fact p.Prime] [Finite R] :
    IsUnit (p : R) â†” Â¬p âˆ£ ringChar R :=
  isUnit_iff_not_dvd_char_of_ringChar_ne_zero R p <| CharP.char_ne_zero_of_finite R (ringChar R)
#align is_unit_iff_not_dvd_char isUnit_iff_not_dvd_char

/-- The prime divisors of the characteristic of a finite commutative ring are exactly
the prime divisors of its cardinality. -/
theorem prime_dvd_char_iff_dvd_card {R : Type*} [CommRing R] [Fintype R] (p : â„•) [Fact p.Prime] :
    p âˆ£ ringChar R â†” p âˆ£ Fintype.card R := by
  refine'
    âŸ¨fun h =>
      h.trans <|
        Int.coe_nat_dvd.mp <|
          (CharP.int_cast_eq_zero_iff R (ringChar R) (Fintype.card R)).mp <| by
            exact_mod_cast CharP.cast_card_eq_zero R,
      fun h => _âŸ©
  by_contra hâ‚€
  -- âŠ¢ False
  rcases exists_prime_addOrderOf_dvd_card p h with âŸ¨r, hrâŸ©
  -- âŠ¢ False
  have hrâ‚ := addOrderOf_nsmul_eq_zero r
  -- âŠ¢ False
  rw [hr, nsmul_eq_mul] at hrâ‚
  -- âŠ¢ False
  rcases IsUnit.exists_left_inv ((isUnit_iff_not_dvd_char R p).mpr hâ‚€) with âŸ¨u, huâŸ©
  -- âŠ¢ False
  apply_fun (Â· * Â·) u at hrâ‚
  -- âŠ¢ False
  rw [mul_zero, â† mul_assoc, hu, one_mul] at hrâ‚
  -- âŠ¢ False
  exact mt AddMonoid.addOrderOf_eq_one_iff.mpr (ne_of_eq_of_ne hr (Nat.Prime.ne_one Fact.out)) hrâ‚
  -- ğŸ‰ no goals
#align prime_dvd_char_iff_dvd_card prime_dvd_char_iff_dvd_card

/-- A prime that does not divide the cardinality of a finite commutative ring `R`
is a unit in `R`. -/
theorem not_isUnit_prime_of_dvd_card {R : Type*} [CommRing R] [Fintype R] (p : â„•) [Fact p.Prime]
    (hp : p âˆ£ Fintype.card R) : Â¬IsUnit (p : R) :=
  mt (isUnit_iff_not_dvd_char R p).mp
    (Classical.not_not.mpr ((prime_dvd_char_iff_dvd_card p).mpr hp))
#align not_is_unit_prime_of_dvd_card not_isUnit_prime_of_dvd_card
