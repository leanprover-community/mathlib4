/-
Copyright (c) 2022 Jon Eugster. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jon Eugster
-/
import Mathlib.Algebra.CharP.Algebra
import Mathlib.Algebra.CharP.LocalRing
import Mathlib.RingTheory.Ideal.Quotient
import Mathlib.Tactic.FieldSimp

#align_import algebra.char_p.mixed_char_zero from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Equal and mixed characteristic

In commutative algebra, some statements are simpler when working over a `â„š`-algebra `R`, in which
case one also says that the ring has "equal characteristic zero". A ring that is not a
`â„š`-algebra has either positive characteristic or there exists a prime ideal `I âŠ‚ R` such that
the quotient `R â§¸ I` has positive characteristic `p > 0`. In this case one speaks of
"mixed characteristic `(0, p)`", where `p` is only unique if `R` is local.

Examples of mixed characteristic rings are `â„¤` or the `p`-adic integers/numbers.

This file provides the main theorem `split_by_characteristic` that splits any proposition `P` into
the following three cases:

1) Positive characteristic: `CharP R p` (where `p â‰  0`)
2) Equal characteristic zero: `Algebra â„š R`
3) Mixed characteristic: `MixedCharZero R p` (where `p` is prime)

## Main definitions

- `MixedCharZero` : A ring has mixed characteristic `(0, p)` if it has characteristic zero
  and there exists an ideal such that the quotient `R â§¸ I` has characteristic `p`.

## Main results

- `split_equalCharZero_mixedCharZero` : Split a statement into equal/mixed characteristic zero.

This main theorem has the following three corollaries which include the positive
characteristic case for convenience:

- `split_by_characteristic` : Generally consider positive char `p â‰  0`.
- `split_by_characteristic_domain` : In a domain we can assume that `p` is prime.
- `split_by_characteristic_localRing` : In a local ring we can assume that `p` is a prime power.

## Implementation Notes

We use the terms `EqualCharZero` and `AlgebraRat` despite not being such definitions in mathlib.
The former refers to the statement `âˆ€ I : Ideal R, I â‰  âŠ¤ â†’ CharZero (R â§¸ I)`, the latter
refers to the existence of an instance `[Algebra â„š R]`. The two are shown to be
equivalent conditions.

## TODO

- Relate mixed characteristic in a local ring to p-adic numbers [NumberTheory.PAdics].
-/

variable (R : Type*) [CommRing R]

/-!
### Mixed characteristic
-/

/--
A ring of characteristic zero is of "mixed characteristic `(0, p)`" if there exists an ideal
such that the quotient `R â§¸ I` has characteristic `p`.

**Remark:** For `p = 0`, `MixedChar R 0` is a meaningless definition (i.e. satisfied by any ring)
as `R â§¸ âŠ¥ â‰… R` has by definition always characteristic zero.
One could require `(I â‰  âŠ¥)` in the definition, but then `MixedChar R 0` would mean something
like `â„¤`-algebra of extension degree `â‰¥ 1` and would be completely independent from
whether something is a `â„š`-algebra or not (e.g. `â„š[X]` would satisfy it but `â„š` wouldn't).
-/
class MixedCharZero (p : â„•) : Prop where
  [toCharZero : CharZero R]
  charP_quotient : âˆƒ I : Ideal R, I â‰  âŠ¤ âˆ§ CharP (R â§¸ I) p
#align mixed_char_zero MixedCharZero

namespace MixedCharZero

/--
Reduction to `p` prime: When proving any statement `P` about mixed characteristic rings we
can always assume that `p` is prime.
-/
theorem reduce_to_p_prime {P : Prop} :
    (âˆ€ p > 0, MixedCharZero R p â†’ P) â†” âˆ€ p : â„•, p.Prime â†’ MixedCharZero R p â†’ P := by
  constructor
  -- âŠ¢ (âˆ€ (p : â„•), p > 0 â†’ MixedCharZero R p â†’ P) â†’ âˆ€ (p : â„•), Nat.Prime p â†’ MixedC â€¦
  Â· intro h q q_prime q_mixedChar
    -- âŠ¢ P
    exact h q (Nat.Prime.pos q_prime) q_mixedChar
    -- ğŸ‰ no goals
  Â· intro h q q_pos q_mixedChar
    -- âŠ¢ P
    rcases q_mixedChar.charP_quotient with âŸ¨I, hI_ne_top, _âŸ©
    -- âŠ¢ P
    -- Krull's Thm: There exists a prime ideal `P` such that `I â‰¤ P`
    rcases Ideal.exists_le_maximal I hI_ne_top with âŸ¨M, hM_max, h_IMâŸ©
    -- âŠ¢ P
    let r := ringChar (R â§¸ M)
    -- âŠ¢ P
    have r_pos : r â‰  0
    -- âŠ¢ r â‰  0
    Â· have q_zero :=
        congr_arg (Ideal.Quotient.factor I M h_IM) (CharP.cast_eq_zero (R â§¸ I) q)
      simp only [map_natCast, map_zero] at q_zero
      -- âŠ¢ r â‰  0
      apply ne_zero_of_dvd_ne_zero (ne_of_gt q_pos)
      -- âŠ¢ r âˆ£ q
      exact (CharP.cast_eq_zero_iff (R â§¸ M) r q).mp q_zero
      -- ğŸ‰ no goals
    have r_prime : Nat.Prime r :=
      or_iff_not_imp_right.1 (CharP.char_is_prime_or_zero (R â§¸ M) r) r_pos
    apply h r r_prime
    -- âŠ¢ MixedCharZero R r
    have : CharZero R := q_mixedChar.toCharZero
    -- âŠ¢ MixedCharZero R r
    exact âŸ¨âŸ¨M, hM_max.ne_top, ringChar.of_eq rflâŸ©âŸ©
    -- ğŸ‰ no goals
#align mixed_char_zero.reduce_to_p_prime MixedCharZero.reduce_to_p_prime

/--
Reduction to `I` prime ideal: When proving statements about mixed characteristic rings,
after we reduced to `p` prime, we can assume that the ideal `I` in the definition is maximal.
-/
theorem reduce_to_maximal_ideal {p : â„•} (hp : Nat.Prime p) :
    (âˆƒ I : Ideal R, I â‰  âŠ¤ âˆ§ CharP (R â§¸ I) p) â†” âˆƒ I : Ideal R, I.IsMaximal âˆ§ CharP (R â§¸ I) p := by
  constructor
  -- âŠ¢ (âˆƒ I, I â‰  âŠ¤ âˆ§ CharP (R â§¸ I) p) â†’ âˆƒ I, Ideal.IsMaximal I âˆ§ CharP (R â§¸ I) p
  Â· intro g
    -- âŠ¢ âˆƒ I, Ideal.IsMaximal I âˆ§ CharP (R â§¸ I) p
    rcases g with âŸ¨I, âŸ¨hI_not_top, _âŸ©âŸ©
    -- âŠ¢ âˆƒ I, Ideal.IsMaximal I âˆ§ CharP (R â§¸ I) p
    -- Krull's Thm: There exists a prime ideal `M` such that `I â‰¤ M`.
    rcases Ideal.exists_le_maximal I hI_not_top with âŸ¨M, âŸ¨hM_max, hM_geâŸ©âŸ©
    -- âŠ¢ âˆƒ I, Ideal.IsMaximal I âˆ§ CharP (R â§¸ I) p
    use M
    -- âŠ¢ Ideal.IsMaximal M âˆ§ CharP (R â§¸ M) p
    constructor
    -- âŠ¢ Ideal.IsMaximal M
    Â· exact hM_max
      -- ğŸ‰ no goals
    Â· cases CharP.exists (R â§¸ M) with
      | intro r hr =>
        -- Porting note: This is odd. Added `have hr := hr`.
        -- Without this it seems that lean does not find `hr` as an instance.
        have hr := hr
        convert hr
        have r_dvd_p : r âˆ£ p
        Â· rw [â† CharP.cast_eq_zero_iff (R â§¸ M) r p]
          convert congr_arg (Ideal.Quotient.factor I M hM_ge) (CharP.cast_eq_zero (R â§¸ I) p)
        symm
        apply (Nat.Prime.eq_one_or_self_of_dvd hp r r_dvd_p).resolve_left
        exact CharP.char_ne_one (R â§¸ M) r
  Â· intro âŸ¨I, hI_max, h_charPâŸ©
    -- âŠ¢ âˆƒ I, I â‰  âŠ¤ âˆ§ CharP (R â§¸ I) p
    use I
    -- âŠ¢ I â‰  âŠ¤ âˆ§ CharP (R â§¸ I) p
    exact âŸ¨Ideal.IsMaximal.ne_top hI_max, h_charPâŸ©
    -- ğŸ‰ no goals
#align mixed_char_zero.reduce_to_maximal_ideal MixedCharZero.reduce_to_maximal_ideal

end MixedCharZero

/-!
### Equal characteristic zero

A commutative ring `R` has "equal characteristic zero" if it satisfies one of the following
equivalent properties:

1) `R` is a `â„š`-algebra.
2) The quotient `R â§¸ I` has characteristic zero for any proper ideal `I âŠ‚ R`.
3) `R` has characteristic zero and does not have mixed characteristic for any prime `p`.

We show `(1) â†” (2) â†” (3)`, and most of the following is concerned with constructing
an explicit algebra map `â„š â†’+* R` (given by `x â†¦ (x.num : R) /â‚š â†‘x.pnatDen`)
for the direction `(1) â† (2)`.

Note: Property `(2)` is denoted as `EqualCharZero` in the statement names below.
-/

namespace EqualCharZero

/-- `â„š`-algebra implies equal characteristic. -/
theorem of_algebraRat [Algebra â„š R] : âˆ€ I : Ideal R, I â‰  âŠ¤ â†’ CharZero (R â§¸ I) := by
  intro I hI
  -- âŠ¢ CharZero (R â§¸ I)
  constructor
  -- âŠ¢ Function.Injective Nat.cast
  intro a b h_ab
  -- âŠ¢ a = b
  contrapose! hI
  -- âŠ¢ I = âŠ¤
  -- `â†‘a - â†‘b` is a unit contained in `I`, which contradicts `I â‰  âŠ¤`.
  refine' I.eq_top_of_isUnit_mem _ (IsUnit.map (algebraMap â„š R) (IsUnit.mk0 (a - b : â„š) _))
  -- âŠ¢ â†‘(algebraMap â„š R) (â†‘a - â†‘b) âˆˆ I
  Â· simpa only [â† Ideal.Quotient.eq_zero_iff_mem, map_sub, sub_eq_zero, map_natCast]
    -- ğŸ‰ no goals
  simpa only [Ne.def, sub_eq_zero] using (@Nat.cast_injective â„š _ _).ne hI
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Q_algebra_to_equal_char_zero EqualCharZero.of_algebraRat

section ConstructionAlgebraRat

variable {R}

/-- Internal: Not intended to be used outside this local construction. -/
theorem PNat.isUnit_natCast [h : Fact (âˆ€ I : Ideal R, I â‰  âŠ¤ â†’ CharZero (R â§¸ I))]
    (n : â„•+) : IsUnit (n : R) := by
  -- `n : R` is a unit iff `(n)` is not a proper ideal in `R`.
  rw [â† Ideal.span_singleton_eq_top]
  -- âŠ¢ Ideal.span {â†‘â†‘n} = âŠ¤
  -- So by contrapositive, we should show the quotient does not have characteristic zero.
  apply not_imp_comm.mp (h.elim (Ideal.span {â†‘n}))
  -- âŠ¢ Â¬CharZero (R â§¸ Ideal.span {â†‘â†‘n})
  intro h_char_zero
  -- âŠ¢ False
  -- In particular, the image of `n` in the quotient should be nonzero.
  apply h_char_zero.cast_injective.ne n.ne_zero
  -- âŠ¢ â†‘â†‘n = â†‘0
  -- But `n` generates the ideal, so its image is clearly zero.
  rw [â† map_natCast (Ideal.Quotient.mk _), Nat.cast_zero, Ideal.Quotient.eq_zero_iff_mem]
  -- âŠ¢ â†‘â†‘n âˆˆ Ideal.span {â†‘â†‘n}
  exact Ideal.subset_span (Set.mem_singleton _)
  -- ğŸ‰ no goals
#align equal_char_zero.pnat_coe_is_unit EqualCharZero.PNat.isUnit_natCast

@[coe]
noncomputable def pnatCast [Fact (âˆ€ I : Ideal R, I â‰  âŠ¤ â†’ CharZero (R â§¸ I))] : â„•+ â†’ RË£ :=
  fun n => (PNat.isUnit_natCast n).unit

/-- Internal: Not intended to be used outside this local construction. -/
noncomputable instance coePNatUnits
    [Fact (âˆ€ I : Ideal R, I â‰  âŠ¤ â†’ CharZero (R â§¸ I))] : Coe â„•+ RË£ :=
  âŸ¨EqualCharZero.pnatCastâŸ©
#align equal_char_zero.pnat_has_coe_units EqualCharZero.coePNatUnits

/-- Internal: Not intended to be used outside this local construction. -/
@[simp]
theorem pnatCast_one [Fact (âˆ€ I : Ideal R, I â‰  âŠ¤ â†’ CharZero (R â§¸ I))] : ((1 : â„•+) : RË£) = 1 := by
  apply Units.ext
  -- âŠ¢ â†‘â†‘1 = â†‘1
  rw [Units.val_one]
  -- âŠ¢ â†‘â†‘1 = 1
  change ((PNat.isUnit_natCast (R := R) 1).unit : R) = 1
  -- âŠ¢ â†‘(IsUnit.unit (_ : IsUnit â†‘â†‘1)) = 1
  rw [IsUnit.unit_spec (PNat.isUnit_natCast 1)]
  -- âŠ¢ â†‘â†‘1 = 1
  rw [PNat.one_coe, Nat.cast_one]
  -- ğŸ‰ no goals
#align equal_char_zero.pnat_coe_units_eq_one EqualCharZero.pnatCast_one

/-- Internal: Not intended to be used outside this local construction. -/
@[simp]
theorem pnatCast_eq_natCast [Fact (âˆ€ I : Ideal R, I â‰  âŠ¤ â†’ CharZero (R â§¸ I))] (n : â„•+) :
    ((n : RË£) : R) = â†‘n := by
  change ((PNat.isUnit_natCast (R := R) n).unit : R) = â†‘n
  -- âŠ¢ â†‘(IsUnit.unit (_ : IsUnit â†‘â†‘n)) = â†‘â†‘n
  simp only [IsUnit.unit_spec]
  -- ğŸ‰ no goals
#align equal_char_zero.pnat_coe_units_coe_eq_coe EqualCharZero.pnatCast_eq_natCast

/-- Equal characteristic implies `â„š`-algebra. -/
noncomputable def algebraRat (h : âˆ€ I : Ideal R, I â‰  âŠ¤ â†’ CharZero (R â§¸ I)) :
    Algebra â„š R :=
  haveI : Fact (âˆ€ I : Ideal R, I â‰  âŠ¤ â†’ CharZero (R â§¸ I)) := âŸ¨hâŸ©
  RingHom.toAlgebra
  { toFun := fun x => x.num /â‚š â†‘x.pnatDen
    map_zero' := by simp [divp]
                    -- ğŸ‰ no goals
                   -- ğŸ‰ no goals
    map_one' := by simp
    map_mul' := by
      -- âŠ¢ OneHom.toFun { toFun := fun x => â†‘x.num /â‚š â†‘(Rat.pnatDen x), map_one' := (_  â€¦
      intro a b
      -- âŠ¢ â†‘(a * b).num * (â†‘b.den * â†‘a.den) = â†‘a.num * â†‘b.num * â†‘(a * b).den
      field_simp
      -- âŠ¢ â†‘(a * b).num * (â†‘b.den * â†‘a.den) = â†‘((a * b).num * â†‘a.den * â†‘b.den)
      trans (â†‘((a * b).num * a.den * b.den) : R)
        -- âŠ¢ â†‘(a * b).num * (â†‘b.den * â†‘a.den) = â†‘(a * b).num * â†‘a.den * â†‘b.den
      Â· simp_rw [Int.cast_mul, Int.cast_ofNat]
        -- ğŸ‰ no goals
        ring
      -- âŠ¢ â†‘(a.num * b.num * â†‘(a * b).den) = â†‘a.num * â†‘b.num * â†‘(a * b).den
      rw [Rat.mul_num_den' a b]
      -- ğŸ‰ no goals
      simp
    map_add' := by
      intro a b
      -- âŠ¢ OneHom.toFun (â†‘{ toOneHom := { toFun := fun x => â†‘x.num /â‚š â†‘(Rat.pnatDen x), â€¦
      field_simp
      -- âŠ¢ â†‘(a + b).num * (â†‘b.den * â†‘a.den) = (â†‘a.num * â†‘b.den + â†‘b.num * â†‘a.den) * â†‘(a â€¦
      trans (â†‘((a + b).num * a.den * b.den) : R)
      -- âŠ¢ â†‘(a + b).num * (â†‘b.den * â†‘a.den) = â†‘((a + b).num * â†‘a.den * â†‘b.den)
      Â· simp_rw [Int.cast_mul, Int.cast_ofNat]
        -- âŠ¢ â†‘(a + b).num * (â†‘b.den * â†‘a.den) = â†‘(a + b).num * â†‘a.den * â†‘b.den
        ring
        -- ğŸ‰ no goals
      rw [Rat.add_num_den' a b]
      -- âŠ¢ â†‘((a.num * â†‘b.den + b.num * â†‘a.den) * â†‘(a + b).den) = (â†‘a.num * â†‘b.den + â†‘b. â€¦
      simp }
      -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align equal_char_zero_to_Q_algebra EqualCharZero.algebraRat

end ConstructionAlgebraRat

/-- Not mixed characteristic implies equal characteristic. -/
theorem of_not_mixedCharZero [CharZero R] (h : âˆ€ p > 0, Â¬MixedCharZero R p) :
    âˆ€ I : Ideal R, I â‰  âŠ¤ â†’ CharZero (R â§¸ I) := by
  intro I hI_ne_top
  -- âŠ¢ CharZero (R â§¸ I)
  suffices h_charP : CharP (R â§¸ I) 0
  -- âŠ¢ CharZero (R â§¸ I)
  Â· apply CharP.charP_to_charZero
    -- ğŸ‰ no goals
  cases CharP.exists (R â§¸ I) with
  | intro p hp =>
    cases p with
    | zero => exact hp
    | succ p =>
      have h_mixed : MixedCharZero R p.succ := âŸ¨âŸ¨I, âŸ¨hI_ne_top, hpâŸ©âŸ©âŸ©
      exact absurd h_mixed (h p.succ p.succ_pos)
#align not_mixed_char_to_equal_char_zero EqualCharZero.of_not_mixedCharZero

/-- Equal characteristic implies not mixed characteristic. -/
theorem to_not_mixedCharZero (h : âˆ€ I : Ideal R, I â‰  âŠ¤ â†’ CharZero (R â§¸ I)) :
    âˆ€ p > 0, Â¬MixedCharZero R p := by
  intro p p_pos
  -- âŠ¢ Â¬MixedCharZero R p
  by_contra hp_mixedChar
  -- âŠ¢ False
  rcases hp_mixedChar.charP_quotient with âŸ¨I, hI_ne_top, hI_pâŸ©
  -- âŠ¢ False
  replace hI_zero : CharP (R â§¸ I) 0 := @CharP.ofCharZero _ _ (h I hI_ne_top)
  -- âŠ¢ False
  exact absurd (CharP.eq (R â§¸ I) hI_p hI_zero) (ne_of_gt p_pos)
  -- ğŸ‰ no goals
#align equal_char_zero_to_not_mixed_char EqualCharZero.to_not_mixedCharZero

/--
A ring of characteristic zero has equal characteristic iff it does not
have mixed characteristic for any `p`.
-/
theorem iff_not_mixedCharZero [CharZero R] :
    (âˆ€ I : Ideal R, I â‰  âŠ¤ â†’ CharZero (R â§¸ I)) â†” âˆ€ p > 0, Â¬MixedCharZero R p :=
  âŸ¨to_not_mixedCharZero R, of_not_mixedCharZero RâŸ©
#align equal_char_zero_iff_not_mixed_char EqualCharZero.iff_not_mixedCharZero

/-- A ring is a `â„š`-algebra iff it has equal characteristic zero. -/
theorem nonempty_algebraRat_iff :
    Nonempty (Algebra â„š R) â†” âˆ€ I : Ideal R, I â‰  âŠ¤ â†’ CharZero (R â§¸ I) := by
  constructor
  -- âŠ¢ Nonempty (Algebra â„š R) â†’ âˆ€ (I : Ideal R), I â‰  âŠ¤ â†’ CharZero (R â§¸ I)
  Â· intro h_alg
    -- âŠ¢ âˆ€ (I : Ideal R), I â‰  âŠ¤ â†’ CharZero (R â§¸ I)
    haveI h_alg' : Algebra â„š R := h_alg.some
    -- âŠ¢ âˆ€ (I : Ideal R), I â‰  âŠ¤ â†’ CharZero (R â§¸ I)
    apply of_algebraRat
    -- ğŸ‰ no goals
  Â· intro h
    -- âŠ¢ Nonempty (Algebra â„š R)
    apply Nonempty.intro
    -- âŠ¢ Algebra â„š R
    exact algebraRat h
    -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Q_algebra_iff_equal_char_zero EqualCharZero.nonempty_algebraRat_iff

end EqualCharZero

/--
A ring of characteristic zero is not a `â„š`-algebra iff it has mixed characteristic for some `p`.
-/
theorem isEmpty_algebraRat_iff_mixedCharZero [CharZero R] :
    IsEmpty (Algebra â„š R) â†” âˆƒ p > 0, MixedCharZero R p := by
  rw [â† not_iff_not]
  -- âŠ¢ Â¬IsEmpty (Algebra â„š R) â†” Â¬âˆƒ p, p > 0 âˆ§ MixedCharZero R p
  push_neg
  -- âŠ¢ Â¬IsEmpty (Algebra â„š R) â†” âˆ€ (p : â„•), p > 0 â†’ Â¬MixedCharZero R p
  rw [not_isEmpty_iff, â† EqualCharZero.iff_not_mixedCharZero]
  -- âŠ¢ Nonempty (Algebra â„š R) â†” âˆ€ (I : Ideal R), I â‰  âŠ¤ â†’ CharZero (R â§¸ I)
  apply EqualCharZero.nonempty_algebraRat_iff
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align not_Q_algebra_iff_not_equal_char_zero isEmpty_algebraRat_iff_mixedCharZero

/-!
# Splitting statements into different characteristic

Statements to split a proof by characteristic. There are 3 theorems here that are very
similar. They only differ in the assumptions we can make on the positive characteristic
case:
Generally we need to consider all `p â‰  0`, but if `R` is a local ring, we can assume
that `p` is a prime power. And if `R` is a domain, we can even assume that `p` is prime.
-/

section MainStatements

variable {P : Prop}

/-- Split a `Prop` in characteristic zero into equal and mixed characteristic. -/
theorem split_equalCharZero_mixedCharZero [CharZero R] (h_equal : Algebra â„š R â†’ P)
    (h_mixed : âˆ€ p : â„•, Nat.Prime p â†’ MixedCharZero R p â†’ P) : P := by
  by_cases h : âˆƒ p > 0, MixedCharZero R p
  -- âŠ¢ P
  Â· rcases h with âŸ¨p, âŸ¨H, hpâŸ©âŸ©
    -- âŠ¢ P
    rw [â† MixedCharZero.reduce_to_p_prime] at h_mixed
    -- âŠ¢ P
    exact h_mixed p H hp
    -- ğŸ‰ no goals
  Â· apply h_equal
    -- âŠ¢ Algebra â„š R
    rw [â† isEmpty_algebraRat_iff_mixedCharZero, not_isEmpty_iff] at h
    -- âŠ¢ Algebra â„š R
    exact h.some
    -- ğŸ‰ no goals
#align split_equal_mixed_char split_equalCharZero_mixedCharZero

example (n : â„•) (h : n â‰  0) : 0 < n :=
  zero_lt_iff.mpr h

/--
Split any `Prop` over `R` into the three cases:
- positive characteristic.
- equal characteristic zero.
- mixed characteristic `(0, p)`.
-/
theorem split_by_characteristic (h_pos : âˆ€ p : â„•, p â‰  0 â†’ CharP R p â†’ P) (h_equal : Algebra â„š R â†’ P)
    (h_mixed : âˆ€ p : â„•, Nat.Prime p â†’ MixedCharZero R p â†’ P) : P := by
  cases CharP.exists R with
  | intro p p_charP =>
    by_cases p = 0
    Â· rw [h] at p_charP
      haveI h0 : CharZero R := CharP.charP_to_charZero R
      exact split_equalCharZero_mixedCharZero R h_equal h_mixed
    Â· exact h_pos p h p_charP
#align split_by_characteristic split_by_characteristic

/--
In an `IsDomain R`, split any `Prop` over `R` into the three cases:
- *prime* characteristic.
- equal characteristic zero.
- mixed characteristic `(0, p)`.
-/
theorem split_by_characteristic_domain [IsDomain R] (h_pos : âˆ€ p : â„•, Nat.Prime p â†’ CharP R p â†’ P)
    (h_equal : Algebra â„š R â†’ P) (h_mixed : âˆ€ p : â„•, Nat.Prime p â†’ MixedCharZero R p â†’ P) : P := by
  refine split_by_characteristic R ?_ h_equal h_mixed
  -- âŠ¢ âˆ€ (p : â„•), p â‰  0 â†’ CharP R p â†’ P
  intro p p_pos p_char
  -- âŠ¢ P
  have p_prime : Nat.Prime p := or_iff_not_imp_right.mp (CharP.char_is_prime_or_zero R p) p_pos
  -- âŠ¢ P
  exact h_pos p p_prime p_char
  -- ğŸ‰ no goals
#align split_by_characteristic_domain split_by_characteristic_domain

 /--
In a `LocalRing R`, split any `Prop` over `R` into the three cases:
- *prime power* characteristic.
- equal characteristic zero.
- mixed characteristic `(0, p)`.
-/
theorem split_by_characteristic_localRing [LocalRing R]
    (h_pos : âˆ€ p : â„•, IsPrimePow p â†’ CharP R p â†’ P) (h_equal : Algebra â„š R â†’ P)
    (h_mixed : âˆ€ p : â„•, Nat.Prime p â†’ MixedCharZero R p â†’ P) : P := by
  refine' split_by_characteristic R _ h_equal h_mixed
  -- âŠ¢ âˆ€ (p : â„•), p â‰  0 â†’ CharP R p â†’ P
  intro p p_pos p_char
  -- âŠ¢ P
  have p_ppow : IsPrimePow (p : â„•) := or_iff_not_imp_left.mp (charP_zero_or_prime_power R p) p_pos
  -- âŠ¢ P
  exact h_pos p p_ppow p_char
  -- ğŸ‰ no goals
#align split_by_characteristic_local_ring split_by_characteristic_localRing

end MainStatements
