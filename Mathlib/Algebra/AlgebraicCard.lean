/-
Copyright (c) 2022 Violeta Hern√°ndez Palacios. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Violeta Hern√°ndez Palacios
-/
import Mathlib.Data.Polynomial.Cardinal
import Mathlib.RingTheory.Algebraic

#align_import algebra.algebraic_card from "leanprover-community/mathlib"@"40494fe75ecbd6d2ec61711baa630cf0a7b7d064"

/-!
### Cardinality of algebraic numbers

In this file, we prove variants of the following result: the cardinality of algebraic numbers under
an R-algebra is at most `# R[X] * ‚Ñµ‚ÇÄ`.

Although this can be used to prove that real or complex transcendental numbers exist, a more direct
proof is given by `Liouville.is_transcendental`.
-/


universe u v

open Cardinal Polynomial Set

open Cardinal Polynomial

namespace Algebraic

theorem infinite_of_charZero (R A : Type*) [CommRing R] [IsDomain R] [Ring A] [Algebra R A]
    [CharZero A] : { x : A | IsAlgebraic R x }.Infinite :=
  infinite_of_injective_forall_mem Nat.cast_injective isAlgebraic_nat
#align algebraic.infinite_of_char_zero Algebraic.infinite_of_charZero

theorem aleph0_le_cardinal_mk_of_charZero (R A : Type*) [CommRing R] [IsDomain R] [Ring A]
    [Algebra R A] [CharZero A] : ‚Ñµ‚ÇÄ ‚â§ #{ x : A // IsAlgebraic R x } :=
  infinite_iff.1 (Set.infinite_coe_iff.2 <| infinite_of_charZero R A)
#align algebraic.aleph_0_le_cardinal_mk_of_char_zero Algebraic.aleph0_le_cardinal_mk_of_charZero

section lift

variable (R : Type u) (A : Type v) [CommRing R] [CommRing A] [IsDomain A] [Algebra R A]
  [NoZeroSMulDivisors R A]

theorem cardinal_mk_lift_le_mul :
    Cardinal.lift.{u} #{ x : A // IsAlgebraic R x } ‚â§ Cardinal.lift.{v} #(R[X]) * ‚Ñµ‚ÇÄ := by
  rw [‚Üê mk_uLift, ‚Üê mk_uLift]
  -- ‚ä¢ #(ULift { x // IsAlgebraic R x }) ‚â§ #(ULift R[X]) * ‚Ñµ‚ÇÄ
  choose g hg‚ÇÅ hg‚ÇÇ using fun x : { x : A | IsAlgebraic R x } => x.coe_prop
  -- ‚ä¢ #(ULift { x // IsAlgebraic R x }) ‚â§ #(ULift R[X]) * ‚Ñµ‚ÇÄ
  refine' lift_mk_le_lift_mk_mul_of_lift_mk_preimage_le g fun f => _
  -- ‚ä¢ lift.{u, v} #‚Üë(g ‚Åª¬π' {f}) ‚â§ ‚Ñµ‚ÇÄ
  rw [lift_le_aleph0, le_aleph0_iff_set_countable]
  -- ‚ä¢ Set.Countable (g ‚Åª¬π' {f})
  suffices : MapsTo (‚Üë) (g ‚Åª¬π' {f}) (f.rootSet A)
  -- ‚ä¢ Set.Countable (g ‚Åª¬π' {f})
  exact this.countable_of_injOn (Subtype.coe_injective.injOn _) (f.rootSet_finite A).countable
  -- ‚ä¢ MapsTo Subtype.val (g ‚Åª¬π' {f}) (rootSet f A)
  rintro x (rfl : g x = f)
  -- ‚ä¢ ‚Üëx ‚àà rootSet (g x) A
  exact mem_rootSet.2 ‚ü®hg‚ÇÅ x, hg‚ÇÇ x‚ü©
  -- üéâ no goals
#align algebraic.cardinal_mk_lift_le_mul Algebraic.cardinal_mk_lift_le_mul

theorem cardinal_mk_lift_le_max :
    Cardinal.lift.{u} #{ x : A // IsAlgebraic R x } ‚â§ max (Cardinal.lift.{v} #R) ‚Ñµ‚ÇÄ :=
  (cardinal_mk_lift_le_mul R A).trans <|
    (mul_le_mul_right' (lift_le.2 cardinal_mk_le_max) _).trans <| by simp
                                                                     -- üéâ no goals
#align algebraic.cardinal_mk_lift_le_max Algebraic.cardinal_mk_lift_le_max

@[simp]
theorem cardinal_mk_lift_of_infinite [Infinite R] :
    Cardinal.lift.{u} #{ x : A // IsAlgebraic R x } = Cardinal.lift.{v} #R :=
  ((cardinal_mk_lift_le_max R A).trans_eq (max_eq_left <| aleph0_le_mk _)).antisymm <|
    lift_mk_le'.2 ‚ü®‚ü®fun x => ‚ü®algebraMap R A x, isAlgebraic_algebraMap _‚ü©, fun _ _ h =>
      NoZeroSMulDivisors.algebraMap_injective R A (Subtype.ext_iff.1 h)‚ü©‚ü©
#align algebraic.cardinal_mk_lift_of_infinite Algebraic.cardinal_mk_lift_of_infinite

variable [Countable R]

@[simp]
protected theorem countable : Set.Countable { x : A | IsAlgebraic R x } := by
  rw [‚Üê le_aleph0_iff_set_countable, ‚Üê lift_le]
  -- ‚ä¢ lift.{?u.44998, v} #‚Üë{x | IsAlgebraic R x} ‚â§ lift.{?u.44998, v} ‚Ñµ‚ÇÄ
  apply (cardinal_mk_lift_le_max R A).trans
  -- ‚ä¢ max (lift.{v, u} #R) ‚Ñµ‚ÇÄ ‚â§ lift.{u, v} ‚Ñµ‚ÇÄ
  simp
  -- üéâ no goals
#align algebraic.countable Algebraic.countable

@[simp]
theorem cardinal_mk_of_countable_of_charZero [CharZero A] [IsDomain R] :
    #{ x : A // IsAlgebraic R x } = ‚Ñµ‚ÇÄ :=
  (Algebraic.countable R A).le_aleph0.antisymm (aleph0_le_cardinal_mk_of_charZero R A)
#align algebraic.cardinal_mk_of_countble_of_char_zero Algebraic.cardinal_mk_of_countable_of_charZero

end lift

section NonLift

variable (R A : Type u) [CommRing R] [CommRing A] [IsDomain A] [Algebra R A]
  [NoZeroSMulDivisors R A]

theorem cardinal_mk_le_mul : #{ x : A // IsAlgebraic R x } ‚â§ #(R[X]) * ‚Ñµ‚ÇÄ := by
  rw [‚Üê lift_id #_, ‚Üê lift_id #(R[X])]
  -- ‚ä¢ lift.{u, u} #{ x // IsAlgebraic R x } ‚â§ lift.{u, u} #R[X] * ‚Ñµ‚ÇÄ
  exact cardinal_mk_lift_le_mul R A
  -- üéâ no goals
#align algebraic.cardinal_mk_le_mul Algebraic.cardinal_mk_le_mul

theorem cardinal_mk_le_max : #{ x : A // IsAlgebraic R x } ‚â§ max #R ‚Ñµ‚ÇÄ := by
  rw [‚Üê lift_id #_, ‚Üê lift_id #R]
  -- ‚ä¢ lift.{u, u} #{ x // IsAlgebraic R x } ‚â§ max (lift.{u, u} #R) ‚Ñµ‚ÇÄ
  exact cardinal_mk_lift_le_max R A
  -- üéâ no goals
#align algebraic.cardinal_mk_le_max Algebraic.cardinal_mk_le_max

@[simp]
theorem cardinal_mk_of_infinite [Infinite R] : #{ x : A // IsAlgebraic R x } = #R :=
  lift_inj.1 <| cardinal_mk_lift_of_infinite R A
#align algebraic.cardinal_mk_of_infinite Algebraic.cardinal_mk_of_infinite

end NonLift

end Algebraic
