/-
Copyright (c) 2020 Anne Baanen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anne Baanen
-/
import Mathlib.Algebra.Group.Units
import Mathlib.Algebra.GroupWithZero.Units.Lemmas
import Mathlib.Algebra.Ring.Defs

#align_import algebra.invertible from "leanprover-community/mathlib"@"722b3b152ddd5e0cf21c0a29787c76596cb6b422"
/-!
# Invertible elements

This file defines a typeclass `Invertible a` for elements `a` with a two-sided
multiplicative inverse.

The intent of the typeclass is to provide a way to write e.g. `‚Öü2` in a ring
like `‚Ñ§[1/2]` where some inverses exist but there is no general `‚Åª¬π` operator;
or to specify that a field has characteristic `‚â† 2`.
It is the `Type`-valued analogue to the `Prop`-valued `IsUnit`.

For constructions of the invertible element given a characteristic, see
`Algebra/Char_P/Invertible` and other lemmas in that file.

## Notation

 * `‚Öüa` is `Invertible.invOf a`, the inverse of `a`

## Implementation notes

The `Invertible` class lives in `Type`, not `Prop`, to make computation easier.
If multiplication is associative, `Invertible` is a subsingleton anyway.

The `simp` normal form tries to normalize `‚Öüa` to `a ‚Åª¬π`. Otherwise, it pushes
`‚Öü` inside the expression as much as possible.

Since `Invertible a` is not a `Prop` (but it is a `Subsingleton`), we have to be careful about
coherence issues: we should avoid having multiple non-defeq instances for `Invertible a` in the
same context.  This file plays it safe and uses `def` rather than `instance` for most definitions,
users can choose which instances to use at the point of use.

For example, here's how you can use an `Invertible 1` instance:
```lean
variables {Œ± : Type*} [Monoid Œ±]

def something_that_needs_inverses (x : Œ±) [Invertible x] := sorry

section
local attribute [instance] invertibleOne
def something_one := something_that_needs_inverses 1
end
```

### Typeclass search vs. unification for `simp` lemmas

Note that since typeclass search searches the local context first, an instance argument like
`[Invertible a]` might sometimes be filled by a different term than the one we'd find by
unification (i.e., the one that's used as an implicit argument to `‚Öü`).

This can cause issues with `simp`. Therefore, some lemmas are duplicated, with the `@[simp]`
versions using unification and the user-facing ones using typeclass search.

Since unification can make backwards rewriting (e.g. `rw [‚Üê mylemma]`) impractical, we still want
the instance-argument versions; therefore the user-facing versions retain the instance arguments
and the original lemma name, whereas the `@[simp]`/unification ones acquire a `'` at the end of
their name.

We modify this file according to the above pattern only as needed; therefore, most `@[simp]` lemmas
here are not part of such a duplicate pair. This is not (yet) intended as a permanent solution.

See Zulip: [https://leanprover.zulipchat.com/#narrow/stream/287929-mathlib4/topic/Invertible.201.20simps/near/320558233]

## Tags

invertible, inverse element, invOf, a half, one half, a third, one third, ¬Ω, ‚Öì

-/

set_option autoImplicit true


universe u

variable {Œ± : Type u}

/-- `Invertible a` gives a two-sided multiplicative inverse of `a`. -/
class Invertible [Mul Œ±] [One Œ±] (a : Œ±) : Type u where
  /-- The inverse of an `Invertible` element -/
  invOf : Œ±
  /-- `invOf a` is a left inverse of `a` -/
  invOf_mul_self : invOf * a = 1
  /-- `invOf a` is a right inverse of `a` -/
  mul_invOf_self : a * invOf = 1
#align invertible Invertible

/-- The inverse of an `Invertible` element -/
prefix:max
  "‚Öü" =>-- This notation has the same precedence as `Inv.inv`.
  Invertible.invOf

@[simp]
theorem invOf_mul_self' [Mul Œ±] [One Œ±] (a : Œ±) {_ : Invertible a} : ‚Öü a * a = 1 :=
  Invertible.invOf_mul_self

theorem invOf_mul_self [Mul Œ±] [One Œ±] (a : Œ±) [Invertible a] : ‚Öü a * a = 1 :=
  Invertible.invOf_mul_self
#align inv_of_mul_self invOf_mul_self

@[simp]
theorem mul_invOf_self' [Mul Œ±] [One Œ±] (a : Œ±) {_ : Invertible a} : a * ‚Öü a = 1 :=
  Invertible.mul_invOf_self

theorem mul_invOf_self [Mul Œ±] [One Œ±] (a : Œ±) [Invertible a] : a * ‚Öü a = 1 :=
  Invertible.mul_invOf_self
#align mul_inv_of_self mul_invOf_self

@[simp]
theorem invOf_mul_self_assoc' [Monoid Œ±] (a b : Œ±) {_ : Invertible a} : ‚Öü a * (a * b) = b := by
  rw [‚Üê mul_assoc, invOf_mul_self, one_mul]
  -- üéâ no goals

theorem invOf_mul_self_assoc [Monoid Œ±] (a b : Œ±) [Invertible a] : ‚Öü a * (a * b) = b := by
  rw [‚Üê mul_assoc, invOf_mul_self, one_mul]
  -- üéâ no goals
#align inv_of_mul_self_assoc invOf_mul_self_assoc

@[simp]
theorem mul_invOf_self_assoc' [Monoid Œ±] (a b : Œ±) {_ : Invertible a} : a * (‚Öü a * b) = b := by
  rw [‚Üê mul_assoc, mul_invOf_self, one_mul]
  -- üéâ no goals

theorem mul_invOf_self_assoc [Monoid Œ±] (a b : Œ±) [Invertible a] : a * (‚Öü a * b) = b := by
  rw [‚Üê mul_assoc, mul_invOf_self, one_mul]
  -- üéâ no goals
#align mul_inv_of_self_assoc mul_invOf_self_assoc

@[simp]
theorem mul_invOf_mul_self_cancel' [Monoid Œ±] (a b : Œ±) {_ : Invertible b} : a * ‚Öü b * b = a := by
  simp [mul_assoc]
  -- üéâ no goals

theorem mul_invOf_mul_self_cancel [Monoid Œ±] (a b : Œ±) [Invertible b] : a * ‚Öü b * b = a := by
  simp [mul_assoc]
  -- üéâ no goals
#align mul_inv_of_mul_self_cancel mul_invOf_mul_self_cancel

@[simp]
theorem mul_mul_invOf_self_cancel' [Monoid Œ±] (a b : Œ±) {_ : Invertible b} : a * b * ‚Öü b = a := by
  simp [mul_assoc]
  -- üéâ no goals

theorem mul_mul_invOf_self_cancel [Monoid Œ±] (a b : Œ±) [Invertible b] : a * b * ‚Öü b = a := by
  simp [mul_assoc]
  -- üéâ no goals
#align mul_mul_inv_of_self_cancel mul_mul_invOf_self_cancel

theorem invOf_eq_right_inv [Monoid Œ±] {a b : Œ±} [Invertible a] (hac : a * b = 1) : ‚Öü a = b :=
  left_inv_eq_right_inv (invOf_mul_self _) hac
#align inv_of_eq_right_inv invOf_eq_right_inv

theorem invOf_eq_left_inv [Monoid Œ±] {a b : Œ±} [Invertible a] (hac : b * a = 1) : ‚Öü a = b :=
  (left_inv_eq_right_inv hac (mul_invOf_self _)).symm
#align inv_of_eq_left_inv invOf_eq_left_inv

theorem invertible_unique {Œ± : Type u} [Monoid Œ±] (a b : Œ±) [Invertible a] [Invertible b]
    (h : a = b) : ‚Öü a = ‚Öü b := by
  apply invOf_eq_right_inv
  -- ‚ä¢ a * ‚Öüb = 1
  rw [h, mul_invOf_self]
  -- üéâ no goals
#align invertible_unique invertible_unique

instance Invertible.subsingleton [Monoid Œ±] (a : Œ±) : Subsingleton (Invertible a) :=
  ‚ü®fun ‚ü®b, hba, hab‚ü© ‚ü®c, _, hac‚ü© => by
    congr
    -- ‚ä¢ b = c
    exact left_inv_eq_right_inv hba hac‚ü©
    -- üéâ no goals
#align invertible.subsingleton Invertible.subsingleton

/-- If `r` is invertible and `s = r` and `si = ‚Öür`, then `s` is invertible with `‚Öüs = si`. -/
def Invertible.copy' [MulOneClass Œ±] {r : Œ±} (hr : Invertible r) (s : Œ±) (si : Œ±) (hs : s = r)
    (hsi : si = ‚Öü r) : Invertible s where
  invOf := si
  invOf_mul_self := by rw [hs, hsi, invOf_mul_self]
                       -- üéâ no goals
  mul_invOf_self := by rw [hs, hsi, mul_invOf_self]
                       -- üéâ no goals
#align invertible.copy' Invertible.copy'

/-- If `r` is invertible and `s = r`, then `s` is invertible. -/
@[reducible]
def Invertible.copy [MulOneClass Œ±] {r : Œ±} (hr : Invertible r) (s : Œ±) (hs : s = r) :
    Invertible s :=
  hr.copy' _ _ hs rfl
#align invertible.copy Invertible.copy

/-- If `a` is invertible and `a = b`, then `‚Öüa = ‚Öüb`. -/
@[congr]
theorem Invertible.congr [Ring Œ±] (a b : Œ±) [Invertible a] [Invertible b] (h : a = b) :
  ‚Öüa = ‚Öüb := by subst h; congr; apply Subsingleton.allEq
                -- ‚ä¢ ‚Öüa = ‚Öüa
                         -- ‚ä¢ inst‚úù¬π = inst‚úù
                                -- üéâ no goals

/-- An `Invertible` element is a unit. -/
@[simps]
def unitOfInvertible [Monoid Œ±] (a : Œ±) [Invertible a] : Œ±À£ where
  val := a
  inv := ‚Öü a
  val_inv := by simp
                -- üéâ no goals
  inv_val := by simp
                -- üéâ no goals
#align unit_of_invertible unitOfInvertible
#align coe_unit_of_invertible val_unitOfInvertible
#align coe_inv_unit_of_invertible val_inv_unitOfInvertible

theorem isUnit_of_invertible [Monoid Œ±] (a : Œ±) [Invertible a] : IsUnit a :=
  ‚ü®unitOfInvertible a, rfl‚ü©
#align is_unit_of_invertible isUnit_of_invertible

/-- Units are invertible in their associated monoid. -/
def Units.invertible [Monoid Œ±] (u : Œ±À£) :
    Invertible (u : Œ±) where
  invOf := ‚Üëu‚Åª¬π
  invOf_mul_self := u.inv_mul
  mul_invOf_self := u.mul_inv
#align units.invertible Units.invertible

@[simp]
theorem invOf_units [Monoid Œ±] (u : Œ±À£) [Invertible (u : Œ±)] : ‚Öü (u : Œ±) = ‚Üëu‚Åª¬π :=
  invOf_eq_right_inv u.mul_inv
#align inv_of_units invOf_units

theorem IsUnit.nonempty_invertible [Monoid Œ±] {a : Œ±} (h : IsUnit a) : Nonempty (Invertible a) :=
  let ‚ü®x, hx‚ü© := h
  ‚ü®x.invertible.copy _ hx.symm‚ü©
#align is_unit.nonempty_invertible IsUnit.nonempty_invertible

/-- Convert `IsUnit` to `Invertible` using `Classical.choice`.

Prefer `casesI h.nonempty_invertible` over `letI := h.invertible` if you want to avoid choice. -/
noncomputable def IsUnit.invertible [Monoid Œ±] {a : Œ±} (h : IsUnit a) : Invertible a :=
  Classical.choice h.nonempty_invertible
#align is_unit.invertible IsUnit.invertible

@[simp]
theorem nonempty_invertible_iff_isUnit [Monoid Œ±] (a : Œ±) : Nonempty (Invertible a) ‚Üî IsUnit a :=
  ‚ü®Nonempty.rec <| @isUnit_of_invertible _ _ _, IsUnit.nonempty_invertible‚ü©
#align nonempty_invertible_iff_is_unit nonempty_invertible_iff_isUnit

/-- Each element of a group is invertible. -/
def invertibleOfGroup [Group Œ±] (a : Œ±) : Invertible a :=
  ‚ü®a‚Åª¬π, inv_mul_self a, mul_inv_self a‚ü©
#align invertible_of_group invertibleOfGroup

@[simp]
theorem invOf_eq_group_inv [Group Œ±] (a : Œ±) [Invertible a] : ‚Öü a = a‚Åª¬π :=
  invOf_eq_right_inv (mul_inv_self a)
#align inv_of_eq_group_inv invOf_eq_group_inv

/-- `1` is the inverse of itself -/
def invertibleOne [Monoid Œ±] : Invertible (1 : Œ±) :=
  ‚ü®1, mul_one _, one_mul _‚ü©
#align invertible_one invertibleOne

@[simp]
theorem invOf_one' [Monoid Œ±] {_ : Invertible (1 : Œ±)} : ‚Öü (1 : Œ±) = 1 :=
  invOf_eq_right_inv (mul_one _)

theorem invOf_one [Monoid Œ±] [Invertible (1 : Œ±)] : ‚Öü (1 : Œ±) = 1 :=
  invOf_eq_right_inv (mul_one _)
#align inv_of_one invOf_one

/-- `-‚Öüa` is the inverse of `-a` -/
def invertibleNeg [Mul Œ±] [One Œ±] [HasDistribNeg Œ±] (a : Œ±) [Invertible a] : Invertible (-a) :=
  ‚ü®-‚Öü a, by simp, by simp‚ü©
            -- üéâ no goals
                     -- üéâ no goals
#align invertible_neg invertibleNeg

@[simp]
theorem invOf_neg [Monoid Œ±] [HasDistribNeg Œ±] (a : Œ±) [Invertible a] [Invertible (-a)] :
    ‚Öü (-a) = -‚Öü a :=
  invOf_eq_right_inv (by simp)
                         -- üéâ no goals
#align inv_of_neg invOf_neg

@[simp]
theorem one_sub_invOf_two [Ring Œ±] [Invertible (2 : Œ±)] : 1 - (‚Öü 2 : Œ±) = ‚Öü 2 :=
  (isUnit_of_invertible (2 : Œ±)).mul_right_inj.1 <| by
    rw [mul_sub, mul_invOf_self, mul_one, ‚Üê one_add_one_eq_two, add_sub_cancel]
    -- üéâ no goals
#align one_sub_inv_of_two one_sub_invOf_two

@[simp]
theorem invOf_two_add_invOf_two [NonAssocSemiring Œ±] [Invertible (2 : Œ±)] :
    (‚Öü 2 : Œ±) + (‚Öü 2 : Œ±) = 1 := by rw [‚Üê two_mul, mul_invOf_self]
                                    -- üéâ no goals
#align inv_of_two_add_inv_of_two invOf_two_add_invOf_two

/-- `a` is the inverse of `‚Öüa`. -/
instance invertibleInvOf [One Œ±] [Mul Œ±] {a : Œ±} [Invertible a] : Invertible (‚Öü a) :=
  ‚ü®a, mul_invOf_self a, invOf_mul_self a‚ü©
#align invertible_inv_of invertibleInvOf

@[simp]
theorem invOf_invOf [Monoid Œ±] (a : Œ±) [Invertible a] [Invertible (‚Öü a)] : ‚Öü (‚Öü a) = a :=
  invOf_eq_right_inv (invOf_mul_self _)
#align inv_of_inv_of invOf_invOf

@[simp]
theorem invOf_inj [Monoid Œ±] {a b : Œ±} [Invertible a] [Invertible b] : ‚Öü a = ‚Öü b ‚Üî a = b :=
  ‚ü®invertible_unique _ _, invertible_unique _ _‚ü©
#align inv_of_inj invOf_inj

/-- `‚Öüb * ‚Öüa` is the inverse of `a * b` -/
def invertibleMul [Monoid Œ±] (a b : Œ±) [Invertible a] [Invertible b] : Invertible (a * b) :=
  ‚ü®‚Öü b * ‚Öü a, by simp [‚Üê mul_assoc], by simp [‚Üê mul_assoc]‚ü©
                 -- üéâ no goals
                                        -- üéâ no goals
#align invertible_mul invertibleMul

@[simp]
theorem invOf_mul [Monoid Œ±] (a b : Œ±) [Invertible a] [Invertible b] [Invertible (a * b)] :
    ‚Öü (a * b) = ‚Öü b * ‚Öü a :=
  invOf_eq_right_inv (by simp [‚Üê mul_assoc])
                         -- üéâ no goals
#align inv_of_mul invOf_mul

/-- A copy of `invertibleMul` for dot notation. -/
@[reducible]
def Invertible.mul [Monoid Œ±] {a b : Œ±} (_ : Invertible a) (_ : Invertible b) :
    Invertible (a * b) :=
  invertibleMul _ _
#align invertible.mul Invertible.mul

theorem mul_right_inj_of_invertible [Monoid Œ±] (c : Œ±) [Invertible c] :
    a * c = b * c ‚Üî a = b :=
  ‚ü®fun h => by simpa using congr_arg (¬∑ * ‚Öüc) h, congr_arg (¬∑ * _)‚ü©
               -- üéâ no goals

theorem mul_left_inj_of_invertible [Monoid Œ±] (c : Œ±) [Invertible c] :
    c * a = c * b ‚Üî a = b :=
  ‚ü®fun h => by simpa using congr_arg (‚Öüc * ¬∑) h, congr_arg (_ * ¬∑)‚ü©
               -- üéâ no goals

theorem invOf_mul_eq_iff_eq_mul_left [Monoid Œ±] [Invertible (c : Œ±)] :
    ‚Öüc * a = b ‚Üî a = c * b := by
  rw [‚Üê mul_left_inj_of_invertible (c := c), mul_invOf_self_assoc]
  -- üéâ no goals

theorem mul_left_eq_iff_eq_invOf_mul [Monoid Œ±] [Invertible (c : Œ±)] :
    c * a = b ‚Üî a = ‚Öüc * b := by
  rw [‚Üê mul_left_inj_of_invertible (c := ‚Öüc), invOf_mul_self_assoc]
  -- üéâ no goals

theorem mul_invOf_eq_iff_eq_mul_right [Monoid Œ±] [Invertible (c : Œ±)] :
    a * ‚Öüc = b ‚Üî a = b * c := by
  rw [‚Üê mul_right_inj_of_invertible (c := c), mul_invOf_mul_self_cancel]
  -- üéâ no goals

theorem mul_right_eq_iff_eq_mul_invOf [Monoid Œ±] [Invertible (c : Œ±)] :
    a * c = b ‚Üî a = b * ‚Öüc := by
  rw [‚Üê mul_right_inj_of_invertible (c := ‚Öüc), mul_mul_invOf_self_cancel]
  -- üéâ no goals

theorem Commute.invOf_right [Monoid Œ±] {a b : Œ±} [Invertible b] (h : Commute a b) :
    Commute a (‚Öü b) :=
  calc
    a * ‚Öü b = ‚Öü b * (b * a * ‚Öü b) := by simp [mul_assoc]
                                        -- üéâ no goals
    _ = ‚Öü b * (a * b * ‚Öü b) := by rw [h.eq]
                                  -- üéâ no goals
    _ = ‚Öü b * a := by simp [mul_assoc]
                      -- üéâ no goals
#align commute.inv_of_right Commute.invOf_right

theorem Commute.invOf_left [Monoid Œ±] {a b : Œ±} [Invertible b] (h : Commute b a) :
    Commute (‚Öü b) a :=
  calc
    ‚Öü b * a = ‚Öü b * (a * b * ‚Öü b) := by simp [mul_assoc]
                                        -- üéâ no goals
    _ = ‚Öü b * (b * a * ‚Öü b) := by rw [h.eq]
                                  -- üéâ no goals
    _ = a * ‚Öü b := by simp [mul_assoc]
                      -- üéâ no goals
#align commute.inv_of_left Commute.invOf_left

theorem commute_invOf {M : Type*} [One M] [Mul M] (m : M) [Invertible m] : Commute m (‚Öü m) :=
  calc
    m * ‚Öü m = 1 := mul_invOf_self m
    _ = ‚Öü m * m := (invOf_mul_self m).symm
#align commute_inv_of commute_invOf

theorem nonzero_of_invertible [MulZeroOneClass Œ±] (a : Œ±) [Nontrivial Œ±] [Invertible a] : a ‚â† 0 :=
  fun ha =>
  zero_ne_one <|
    calc
      0 = ‚Öü a * a := by simp [ha]
                        -- üéâ no goals
      _ = 1 := invOf_mul_self a
#align nonzero_of_invertible nonzero_of_invertible

theorem pos_of_invertible_cast [Semiring Œ±] [Nontrivial Œ±] (n : ‚Ñï) [Invertible (n : Œ±)] : 0 < n :=
  Nat.zero_lt_of_ne_zero fun h => nonzero_of_invertible (n : Œ±) (h ‚ñ∏ Nat.cast_zero)

instance (priority := 100) Invertible.ne_zero [MulZeroOneClass Œ±] [Nontrivial Œ±] (a : Œ±)
    [Invertible a] : NeZero a :=
  ‚ü®nonzero_of_invertible a‚ü©
#align invertible.ne_zero Invertible.ne_zero

section Monoid

variable [Monoid Œ±]

/-- This is the `Invertible` version of `Units.isUnit_units_mul` -/
@[reducible]
def invertibleOfInvertibleMul (a b : Œ±) [Invertible a] [Invertible (a * b)] : Invertible b where
  invOf := ‚Öü (a * b) * a
  invOf_mul_self := by rw [mul_assoc, invOf_mul_self]
                       -- üéâ no goals
  mul_invOf_self := by
    rw [‚Üê (isUnit_of_invertible a).mul_right_inj, ‚Üê mul_assoc, ‚Üê mul_assoc, mul_invOf_self, mul_one,
      one_mul]
#align invertible_of_invertible_mul invertibleOfInvertibleMul

/-- This is the `Invertible` version of `Units.isUnit_mul_units` -/
@[reducible]
def invertibleOfMulInvertible (a b : Œ±) [Invertible (a * b)] [Invertible b] : Invertible a where
  invOf := b * ‚Öü (a * b)
  invOf_mul_self := by
    rw [‚Üê (isUnit_of_invertible b).mul_left_inj, mul_assoc, mul_assoc, invOf_mul_self, mul_one,
      one_mul]
  mul_invOf_self := by rw [‚Üê mul_assoc, mul_invOf_self]
                       -- üéâ no goals
#align invertible_of_mul_invertible invertibleOfMulInvertible

/-- `invertibleOfInvertibleMul` and `invertibleMul` as an equivalence. -/
@[simps apply symm_apply]
def Invertible.mulLeft {a : Œ±} (_ : Invertible a) (b : Œ±) : Invertible b ‚âÉ Invertible (a * b) where
  toFun _ := invertibleMul a b
  invFun _ := invertibleOfInvertibleMul a _
  left_inv _ := Subsingleton.elim _ _
  right_inv _ := Subsingleton.elim _ _
#align invertible.mul_left Invertible.mulLeft
#align invertible.mul_left_apply Invertible.mulLeft_apply
#align invertible.mul_left_symm_apply Invertible.mulLeft_symm_apply

/-- `invertibleOfMulInvertible` and `invertibleMul` as an equivalence. -/
@[simps apply symm_apply]
def Invertible.mulRight (a : Œ±) {b : Œ±} (_ : Invertible b) : Invertible a ‚âÉ Invertible (a * b) where
  toFun _ := invertibleMul a b
  invFun _ := invertibleOfMulInvertible _ b
  left_inv _ := Subsingleton.elim _ _
  right_inv _ := Subsingleton.elim _ _
#align invertible.mul_right Invertible.mulRight
#align invertible.mul_right_apply Invertible.mulRight_apply
#align invertible.mul_right_symm_apply Invertible.mulRight_symm_apply

end Monoid

section MonoidWithZero

variable [MonoidWithZero Œ±]

/-- A variant of `Ring.inverse_unit`. -/
@[simp]
theorem Ring.inverse_invertible (x : Œ±) [Invertible x] : Ring.inverse x = ‚Öü x :=
  Ring.inverse_unit (unitOfInvertible _)
#align ring.inverse_invertible Ring.inverse_invertible

end MonoidWithZero

section GroupWithZero

variable [GroupWithZero Œ±]

/-- `a‚Åª¬π` is an inverse of `a` if `a ‚â† 0` -/
def invertibleOfNonzero {a : Œ±} (h : a ‚â† 0) : Invertible a :=
  ‚ü®a‚Åª¬π, inv_mul_cancel h, mul_inv_cancel h‚ü©
#align invertible_of_nonzero invertibleOfNonzero

@[simp]
theorem invOf_eq_inv (a : Œ±) [Invertible a] : ‚Öü a = a‚Åª¬π :=
  invOf_eq_right_inv (mul_inv_cancel (nonzero_of_invertible a))
#align inv_of_eq_inv invOf_eq_inv

@[simp]
theorem inv_mul_cancel_of_invertible (a : Œ±) [Invertible a] : a‚Åª¬π * a = 1 :=
  inv_mul_cancel (nonzero_of_invertible a)
#align inv_mul_cancel_of_invertible inv_mul_cancel_of_invertible

@[simp]
theorem mul_inv_cancel_of_invertible (a : Œ±) [Invertible a] : a * a‚Åª¬π = 1 :=
  mul_inv_cancel (nonzero_of_invertible a)
#align mul_inv_cancel_of_invertible mul_inv_cancel_of_invertible

@[simp]
theorem div_mul_cancel_of_invertible (a b : Œ±) [Invertible b] : a / b * b = a :=
  div_mul_cancel a (nonzero_of_invertible b)
#align div_mul_cancel_of_invertible div_mul_cancel_of_invertible

@[simp]
theorem mul_div_cancel_of_invertible (a b : Œ±) [Invertible b] : a * b / b = a :=
  mul_div_cancel a (nonzero_of_invertible b)
#align mul_div_cancel_of_invertible mul_div_cancel_of_invertible

@[simp]
theorem div_self_of_invertible (a : Œ±) [Invertible a] : a / a = 1 :=
  div_self (nonzero_of_invertible a)
#align div_self_of_invertible div_self_of_invertible

/-- `b / a` is the inverse of `a / b` -/
def invertibleDiv (a b : Œ±) [Invertible a] [Invertible b] : Invertible (a / b) :=
  ‚ü®b / a, by simp [‚Üê mul_div_assoc], by simp [‚Üê mul_div_assoc]‚ü©
             -- üéâ no goals
                                        -- üéâ no goals
#align invertible_div invertibleDiv

-- Porting note: removed `simp` attribute as `simp` can prove it
theorem invOf_div (a b : Œ±) [Invertible a] [Invertible b] [Invertible (a / b)] :
    ‚Öü (a / b) = b / a :=
  invOf_eq_right_inv (by simp [‚Üê mul_div_assoc])
                         -- üéâ no goals
#align inv_of_div invOf_div

/-- `a` is the inverse of `a‚Åª¬π` -/
def invertibleInv {a : Œ±} [Invertible a] : Invertible a‚Åª¬π :=
  ‚ü®a, by simp, by simp‚ü©
         -- üéâ no goals
                  -- üéâ no goals
#align invertible_inv invertibleInv

end GroupWithZero

/-- Monoid homs preserve invertibility. -/
def Invertible.map {R : Type*} {S : Type*} {F : Type*} [MulOneClass R] [MulOneClass S]
    [MonoidHomClass F R S] (f : F) (r : R) [Invertible r] :
    Invertible (f r) where
  invOf := f (‚Öü r)
  invOf_mul_self := by rw [‚Üê map_mul, invOf_mul_self, map_one]
                       -- üéâ no goals
  mul_invOf_self := by rw [‚Üê map_mul, mul_invOf_self, map_one]
                       -- üéâ no goals
#align invertible.map Invertible.map

/-- Note that the `Invertible (f r)` argument can be satisfied by using `letI := Invertible.map f r`
before applying this lemma. -/
theorem map_invOf {R : Type*} {S : Type*} {F : Type*} [MulOneClass R] [Monoid S]
    [MonoidHomClass F R S] (f : F) (r : R) [Invertible r] [ifr : Invertible (f r)] :
    f (‚Öü r) = ‚Öü (f r) :=
  have h : ifr = Invertible.map f r := Subsingleton.elim _ _
  by subst h; rfl
     -- ‚ä¢ ‚Üëf ‚Öür = ‚Öü(‚Üëf r)
              -- üéâ no goals
#align map_inv_of map_invOf

/-- If a function `f : R ‚Üí S` has a left-inverse that is a monoid hom,
  then `r : R` is invertible if `f r` is.

The inverse is computed as `g (‚Öü(f r))` -/
@[simps! (config := .lemmasOnly)]
def Invertible.ofLeftInverse {R : Type*} {S : Type*} {G : Type*} [MulOneClass R] [MulOneClass S]
    [MonoidHomClass G S R] (f : R ‚Üí S) (g : G) (r : R) (h : Function.LeftInverse g f)
    [Invertible (f r)] : Invertible r :=
  (Invertible.map g (f r)).copy _ (h r).symm
#align invertible.of_left_inverse Invertible.ofLeftInverse
#align invertible.of_left_inverse_inv_of Invertible.ofLeftInverse_invOf

/-- Invertibility on either side of a monoid hom with a left-inverse is equivalent. -/
@[simps]
def invertibleEquivOfLeftInverse {R : Type*} {S : Type*} {F G : Type*} [Monoid R] [Monoid S]
    [MonoidHomClass F R S] [MonoidHomClass G S R] (f : F) (g : G) (r : R)
    (h : Function.LeftInverse g f) :
    Invertible (f r) ‚âÉ
      Invertible r where
  toFun _ := Invertible.ofLeftInverse f _ _ h
  invFun _ := Invertible.map f _
  left_inv _ := Subsingleton.elim _ _
  right_inv _ := Subsingleton.elim _ _
#align invertible_equiv_of_left_inverse invertibleEquivOfLeftInverse
#align invertible_equiv_of_left_inverse_symm_apply invertibleEquivOfLeftInverse_symm_apply
#align invertible_equiv_of_left_inverse_apply invertibleEquivOfLeftInverse_apply
