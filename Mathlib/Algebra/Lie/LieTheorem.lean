/-
Copyright (c) 2024 Lucas Whitfield. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Lucas Whitfield
-/
import Mathlib.Algebra.Lie.Solvable
import Mathlib.Algebra.Lie.Weights.Basic

/-!
# Lie's theorem for Solvable Lie algebras.

This file proves Lie's theorem, the statement that Lie modules of solvable Lie algebras over
algebraically closed fields of characteristic 0 have a common eigenvector for the action of all
elements of the Lie algebra. This result is named `LieModule.exists_forall_lie_eq_smul_of_Solvable`.
-/

open LieAlgebra

-- let k be a field of characteristic zero
variable {k : Type*} [Field k] [CharZero k]
-- Let L be a Lie algebra over k
variable {L : Type*} [LieRing L] [LieAlgebra k L]
-- and let V be a finite-dimensional k-representation of L
variable {V : Type*} [AddCommGroup V] [Module k V] [FiniteDimensional k V]
  [LieRingModule L V] [LieModule k L V] [Nontrivial V]

open FiniteDimensional

/- We define the Submodules generated by repeatedly applying a linear map `f: V →ₗ[F] V`
to a vector `v`-/

namespace LinearMap.End

variable {R M : Type*} [Semiring R] [AddCommMonoid M] [Module R M]
variable (x : M) (f : Module.End R M)

/-- The submodule generated by `v`, `f v`, `f f v`, ... , `f^[n-1] v`.-/
abbrev iteratedRange (n : ℕ) : Submodule R M :=
  Submodule.span R {(f^a) x | a < n}

@[simp]
lemma iteratedRange_zero_eq_bot : iteratedRange x f 0 = ⊥ := by
  rw [Submodule.span_eq_bot]
  intro x ⟨a, ha, _⟩
  contradiction

lemma iteratedRange_mono {a b : ℕ} (h : a ≤ b) : iteratedRange x f a ≤ iteratedRange x f b :=
  Submodule.span_mono (fun _ ⟨c, hc, hw⟩ ↦ ⟨c, lt_of_lt_of_le hc h, hw⟩)

lemma map_iteratedRange_le (n : ℕ) :
    Submodule.map f (iteratedRange x f n) ≤ iteratedRange x f (n + 1) := by
  rw [Submodule.map_span]
  apply Submodule.span_mono
  rintro _ ⟨_, ⟨m, hm, rfl⟩, rfl⟩
  use m + 1, Nat.add_lt_add_right hm 1
  rw [pow_succ', LinearMap.mul_apply]

/-- The union of `iteratedRange` for all `n : ℕ`.-/
abbrev iSup_iteratedRange : Submodule R M := ⨆ k : ℕ, iteratedRange x f k

lemma map_iteratedRange_iSup_le_iSup :
    Submodule.map f (iSup_iteratedRange x f) ≤ iSup_iteratedRange x f := by
  rw [Submodule.map_iSup, iSup_le_iff]
  intro i
  exact (map_iteratedRange_le x f i).trans (le_iSup _ _)

end LinearMap.End

open LinearMap.End

-- the lie action of `L` on `V`
local notation "π" => LieModule.toEnd k L V

section

variable (A : LieIdeal k L) (χ : Module.Dual k A)

/-- Temporary definition until we clear up `altWeightSpace`.

See also `LieModule.shiftedWeightSpace`. -/
abbrev T (w : A) : Module.End k V := (π w) - χ w • 1

variable (z : L) (w : A) {v : V} (hv : ∀ w : A, ⁅w.val, v⁆ = (χ w) • v)

lemma T_apply_succ (n : ℕ) :
    Submodule.map (T A χ w) (iteratedRange v (π z) (n + 1)) ≤ iteratedRange v (π z) n := by
  rw [Submodule.map_span, Submodule.span_le, Set.image_subset_iff]
  simp only [Set.subset_def, Set.mem_setOf_eq, Set.mem_preimage, SetLike.mem_coe,
    forall_exists_index, and_imp, forall_apply_eq_imp_iff₂]
  induction n generalizing w
  · simp only [zero_add, Nat.lt_one_iff, LinearMap.sub_apply, LieModule.toEnd_apply_apply,
      LinearMap.smul_apply, LinearMap.one_apply, forall_eq, pow_zero, hv w, sub_self, zero_mem]
  · next n hn =>
    intro m hm
    obtain (hm | rfl) : m < n + 1 ∨ m = n + 1 := by omega
    · exact iteratedRange_mono v _ (Nat.le_succ n) (hn w m hm)
    let wz : A := ⟨⁅w, z⁆, lie_mem_left k L A w.val z w.prop⟩
    have hwz : wz.val = ⁅w.val, z⁆ := rfl
    have H : ∀ w, ⁅w.val, (π z ^ n) v⁆ = (T A χ w) ((π z ^ n) v) + χ w • ((π z ^ n) v) := by simp
    rw [T, LinearMap.sub_apply, pow_succ', LinearMap.mul_apply, LieModule.toEnd_apply_apply,
      LieModule.toEnd_apply_apply, LinearMap.smul_apply, LinearMap.one_apply, leibniz_lie,
      ← hwz, H, H, lie_add, lie_smul, add_sub_assoc, add_sub_assoc, sub_self, add_zero]
    refine add_mem (add_mem ?_ ?_) ?_
    · exact iteratedRange_mono v _ n.le_succ (hn wz n n.lt_succ_self)
    · exact Submodule.smul_mem _ _ (Submodule.subset_span ⟨n, n.lt_succ_self, rfl⟩)
    · exact map_iteratedRange_le _ _ _ <| Submodule.mem_map_of_mem <| hn w n n.lt_succ_self

lemma T_map_iSup_iteratedRange' :
    Submodule.map (T A χ w) (iSup_iteratedRange v (π z)) ≤ iSup_iteratedRange v (π z) := by
  rw [Submodule.map_iSup, iSup_le_iff]
  rintro (_|i)
  · simp [Submodule.map_span]
  · exact (T_apply_succ A χ z w hv i).trans (le_iSup _ _)

lemma T_map_iSup_iteratedRange (x : V)
    (hx : x ∈ iSup_iteratedRange v (π z)) : (T A χ w) x ∈ iSup_iteratedRange v (π z) :=
  T_map_iSup_iteratedRange' A χ z w hv <| Submodule.mem_map_of_mem hx

def iSupIR : LieSubmodule k A V where
  toSubmodule := iSup_iteratedRange v (π z)
  lie_mem {w} x hx := by
    simp only [AddSubsemigroup.mem_carrier, AddSubmonoid.mem_toSubsemigroup, Submodule.mem_toAddSubmonoid]
    have hx' : ⁅w, x⁆ = (T A χ w) x + χ w • x := by simp
    rw [hx']
    exact add_mem (T_map_iSup_iteratedRange A χ z w hv x hx)
      (Submodule.smul_mem (iSup_iteratedRange v (π z)) _ hx)

lemma T_map_iteratedRange_nilpotent (N : ℕ) :
    ∀ x ∈ (iteratedRange v (π z)) N, (T A χ w ^ N) x = 0 := by
  induction' N with N ih
  · simp only [iteratedRange_zero_eq_bot, Submodule.mem_bot, pow_zero,
      LinearMap.one_apply, imp_self, forall_const]
  · intro x hx
    rw [pow_succ, LinearMap.mul_apply, ih]
    exact T_apply_succ A χ z w hv N <| Submodule.mem_map_of_mem hx

theorem T_res_nilpotent :
    IsNilpotent ((T A χ w).restrict (T_map_iSup_iteratedRange A χ z w hv)) := by
  suffices iSup_iteratedRange v (π z) ≤ Module.End.maxGenEigenspace (T A χ w) 0 by
    rw [Module.Finite.Module.End.isNilpotent_iff_of_finite]
    intro x
    obtain ⟨n, hn⟩ : ∃ n : ℕ, (T A χ w ^ n) x.1 = 0 := by
      simpa [Module.End.mem_maxGenEigenspace, zero_smul, sub_zero] using this x.2
    use n
    rw [LinearMap.pow_restrict, Subtype.ext_iff, LinearMap.restrict_apply, Subtype.coe_mk, hn,
      ZeroMemClass.coe_zero]
  apply iSup_le
  intro i x hx
  simp only [Module.End.mem_maxGenEigenspace, zero_smul, sub_zero]
  use i
  exact T_map_iteratedRange_nilpotent A χ z w hv i x hx

lemma trace_T_res_zero :
    LinearMap.trace k (iSup_iteratedRange v (π z))
      ((T A χ w).restrict (T_map_iSup_iteratedRange A χ z w hv)) = 0 := by
  apply IsNilpotent.eq_zero
  exact LinearMap.isNilpotent_trace_of_isNilpotent (T_res_nilpotent A χ z w hv)

lemma trace_πza (a : A) :
    LinearMap.trace k (iSupIR A χ z hv) (LieModule.toEnd k A _ ⁅z, a⁆) =
      χ ⁅z, a⁆ • (finrank k (iSupIR A χ z hv)) := by
  rw [← LinearMap.trace_id, ← LinearMap.map_smul, ← sub_eq_zero, ← LinearMap.map_sub]
  exact trace_T_res_zero A χ z ⁅z, a⁆ hv

theorem trace_πza_zero (a : A) :
    LinearMap.trace k (iSupIR A χ z hv) (LieModule.toEnd k A _ ⁅z, a⁆) = 0 := by
  set U := iSupIR A χ z hv
  have hzU : ∀ x ∈ U, (π z) x ∈ U :=
    fun _ hx ↦ (map_iteratedRange_iSup_le_iSup v (π z)) (Submodule.mem_map_of_mem hx)
  have hres : LieModule.toEnd k A U ⁅z, a⁆ = ⁅(π z).restrict hzU, LieModule.toEnd k A U a⁆ := by
    ext ⟨x, hx⟩
    simp only [LieModule.toEnd_apply_apply, LieSubmodule.coe_bracket,
      LieIdeal.coe_bracket_of_module, lie_lie, LieHom.lie_apply, Module.End.lie_apply,
      AddSubgroupClass.coe_sub, LinearMap.restrict_coe_apply]
  rw [hres, LieRing.of_associative_ring_bracket, map_sub, LinearMap.trace_mul_comm, sub_self]

lemma chi_za_zero (a : A) (hv' : v ≠ 0) :
    χ ⁅z, a⁆ = 0 := by
  have h := trace_πza A χ z hv a
  rw [trace_πza_zero A χ z hv a] at h
  suffices h' : finrank k ↥(iSupIR A χ z hv).toSubmodule ≠ 0 by
    aesop
  have hvU : v ∈ iSupIR A χ z hv := by
    apply Submodule.mem_iSup_of_mem 1
    apply Submodule.subset_span
    use 0, zero_lt_one
    rw [pow_zero, LinearMap.one_apply]
  have iSup_iteratedRange_nontrivial : Nontrivial (iSupIR A χ z hv) :=
    ⟨⟨v,hvU⟩,0, by simp only [ne_eq, Submodule.mk_eq_zero, hv', not_false_eq_true]⟩
  apply Nat.ne_of_lt'
  apply FiniteDimensional.finrank_pos

/-- The common eigenvectors of `V` with respect to the action of all elements in
the Lie ideal `A`.-/
private def altWeightSpace' : Submodule k V :=
  (⨅ w : A, (LinearMap.ker (M := V) (T A χ w))).copy {v | ∀ a : A, ⁅a.val, v⁆ = (χ a) • v} <| by
  ext v
  simp only [Subtype.forall, Set.mem_setOf_eq, T, Submodule.iInf_coe, Set.mem_iInter,
    SetLike.mem_coe, LinearMap.mem_ker, LinearMap.sub_apply, LieModule.toEnd_apply_apply,
    LinearMap.smul_apply, LinearMap.one_apply, sub_eq_zero]

/-- The common eigenvectors of `V` with respect to the action of all elements in
the Lie ideal `A`.-/
def altWeightSpace : LieSubmodule k L V where
  toSubmodule := altWeightSpace' A χ
  lie_mem {z v} hv := by
    intro a
    rcases eq_or_ne v 0 with (rfl | hv')
    · simp only [lie_zero, smul_zero]
    · have aux : ⁅a.val, z⁆ = (-⁅z, a⁆).val := by
        simp only [NegMemClass.coe_neg, LieSubmodule.coe_bracket, lie_skew]
      rw [leibniz_lie, hv a, lie_smul, aux, hv, map_neg,
        chi_za_zero A χ z hv a hv', neg_zero, zero_smul, zero_add]

-- move this
instance : IsCoatomic (Submodule k V) :=
  isCoatomic_of_isAtomic_of_complementedLattice_of_isModular

end

-- move this
lemma derivedSeries_eq_top (n : ℕ) (h : derivedSeries k L 1 = ⊤) : derivedSeries k L n = ⊤ := by
  rw [derivedSeries_def]
  induction' n with n ih
  · simp only [Nat.zero_eq, derivedSeriesOfIdeal_zero]
  · rw [derivedSeriesOfIdeal_succ, ih]
    assumption

-- move this
variable (k) (L) in
theorem derivedSeries_ne_top_of_solvable [IsSolvable k L] [Nontrivial L] :
    derivedSeries k L 1 ≠ ⊤ := by
  by_contra!
  rcases LieAlgebra.IsSolvable.solvable (R := k) (L := L) with ⟨n, hn⟩
  apply derivedSeries_eq_top n at this
  aesop

-- move this
theorem exists_lieIdeal_of_derivedSeries_le (A : Submodule k L) (h : derivedSeries k L 1 ≤ A) :
    ∃ A' : LieIdeal k L, A = A' := by
  refine ⟨⟨A, ?_⟩, rfl⟩
  intro x m _
  exact h <| LieSubmodule.lie_mem_lie _ _ (LieSubmodule.mem_top _) (LieSubmodule.mem_top _)

section

open LieModule in
theorem extend_weight [LieModule.IsTriangularizable k L V] (A : LieIdeal k L) (z : L) (hz : z ∉ A)
    (hcompl : IsCompl A.toSubmodule (k ∙ z)) (χ' : Module.Dual k A) (v : V)
    (hv : v ≠ 0) (hvA' : ∀ (x : A), ⁅x, v⁆ = χ' x • v) :
    ∃ (χ : Module.Dual k L) (v : V), v ≠ 0 ∧ ∀ (x : L), ⁅x, v⁆ = χ x • v := by
  set Vχ' := altWeightSpace (V := V) A χ'
  obtain ⟨c, hc⟩ : ∃ c, Module.End.HasEigenvalue (toEnd k L Vχ' z) c := by
    have : Nontrivial Vχ' := nontrivial_of_ne ⟨v, hvA'⟩ 0 <| Subtype.coe_ne_coe.mp hv
    apply Module.End.exists_hasEigenvalue_of_iSup_genEigenspace_eq_top
    exact LieModule.IsTriangularizable.iSup_eq_top z
  obtain ⟨⟨v', hv'⟩, hv''⟩ := Module.End.HasEigenvalue.exists_hasEigenvector hc
  have hz' : z ≠ 0 := by
    rintro rfl
    apply hz <| A.zero_mem

  let π1 := A.toSubmodule.linearProjOfIsCompl (k ∙ z) hcompl
  let π2 := (k ∙ z).linearProjOfIsCompl ↑A hcompl.symm

  use (χ'.comp π1) + c • ((LinearEquiv.toSpanNonzeroSingleton k _ z hz').symm.comp π2), v'
  constructor
  · have := hv''.right
    rw [ne_eq]
    intro h
    apply this
    apply (Submodule.mk_eq_zero _ _).mpr h
  · intro x
    have π1_add_π2 : (π1 x : L) + π2 x = x :=
      Submodule.linear_proj_add_linearProjOfIsCompl_eq_self hcompl x
    nth_rw 1 [← π1_add_π2, add_lie]
    rw [hv' (π1 x), LinearMap.add_apply, LinearMap.comp_apply, add_smul, add_right_inj]
    simp only [LieIdeal.coe_to_lieSubalgebra_to_submodule, LinearMap.smul_apply, LinearMap.coe_comp,
      LinearEquiv.coe_coe, Function.comp_apply, smul_eq_mul]
    rcases Submodule.mem_span_singleton.mp ((π2 x).prop) with ⟨d, hd⟩
    rw [← hd, smul_lie]
    have : ⁅z, v'⁆ = (toEnd k L Vχ' z) ⟨v', hv'⟩ := rfl
    rw [this, Module.End.HasEigenvector.apply_eq_smul hv'', mul_comm, mul_smul]
    simp only [SetLike.mk_smul_mk]
    congr 2
    rw [LinearEquiv.eq_symm_apply]
    ext
    exact hd

-- move this
lemma LieIdeal.incl_injective (I : LieIdeal k L) : Function.Injective I.incl :=
  Subtype.val_injective

theorem LieModule.exists_forall_lie_eq_smul_finrank :
    ∀ (L : Type*) [LieRing L] [LieAlgebra k L] [FiniteDimensional k L] [IsSolvable k L]
      [LieRingModule L V] [LieModule k L V] [LieModule.IsTriangularizable k L V],
      ∃ χ : Module.Dual k L, ∃ v : V, v ≠ 0 ∧ ∀ x : L, ⁅x, v⁆ = χ x • v := by
  intro L _ _ _ _ _ _ _
  obtain _inst|_inst := subsingleton_or_nontrivial L
  · rcases (exists_ne (0 : V)) with ⟨v, hv⟩
    use 0, v, hv
    intro x
    simp only [Subsingleton.elim x 0, zero_lie, map_zero, zero_smul]
  obtain H|⟨A, hcoatomA, hAL⟩ := eq_top_or_exists_le_coatom (derivedSeries k L 1).toSubmodule
  · rw [LieSubmodule.coeSubmodule_eq_top_iff] at H
    exact (derivedSeries_ne_top_of_solvable k L H).elim
  obtain ⟨z, hz⟩ : ∃ (z : L), z ∉ A := by
    by_contra! h
    apply hcoatomA.1
    rw [eq_top_iff]
    exact fun ⦃x⦄ _ => h x
  have hcompl : IsCompl A (k ∙ z) := by
    apply IsCompl.of_le
    · rintro x ⟨(ha : x ∈ A), (hb : x ∈ Submodule.span k _)⟩
      obtain ⟨c, rfl⟩ : ∃ c, c • z = x := by rwa [Submodule.mem_span_singleton] at hb
      apply A.smul_mem c⁻¹ at ha
      rcases eq_or_ne c 0 with (rfl | hc) <;> simp_all
    · apply (hcoatomA.2 _ _).ge
      rw [left_lt_sup]
      contrapose! hz with h
      exact h <| Submodule.mem_span_singleton_self z
  obtain ⟨A, rfl⟩ : ∃ A' : LieIdeal k L, A = A'.toSubmodule :=
    exists_lieIdeal_of_derivedSeries_le _ hAL
  have hAsolv : LieAlgebra.IsSolvable k A := A.incl_injective.lieAlgebra_isSolvable
  obtain ⟨χ', v, hv, hvA⟩ := LieModule.exists_forall_lie_eq_smul_finrank A
  apply extend_weight A z hz hcompl χ' v hv hvA
termination_by L _ _ _ => finrank k L
decreasing_by
  simp_wf
  rw [← finrank_top k L]
  apply Submodule.finrank_lt_finrank_of_lt
  rw [← covBy_top_iff] at hcoatomA
  exact hcoatomA.1

-- If `L` is solvable, we can find a non-zero eigenvector
theorem LieModule.exists_forall_lie_eq_smul_of_Solvable
    [IsSolvable k L] [LieModule.IsTriangularizable k L V] :
    ∃ χ : Module.Dual k L, ∃ v : V, v ≠ 0 ∧ ∀ x : L, ⁅x, v⁆ = χ x • v := by
  let imL := (LieModule.toEnd k L V).range
  have hdim : FiniteDimensional k imL := Submodule.finiteDimensional_of_le (le_top)
  suffices h : ∃ χ : Module.Dual k imL, ∃ v : V, v ≠ 0 ∧ ∀ x : imL, ⁅x, v⁆ = χ x • v by
    rcases h with ⟨χ', v, hv, hχ'⟩
    let toEndo : L →ₗ[k] imL := LinearMap.codRestrict imL.toSubmodule (LieModule.toEnd k L V)
        (fun x ↦ LinearMap.mem_range.mpr ⟨x, rfl⟩ : ∀ x : L, LieModule.toEnd k L V x ∈ imL)
    use χ'.comp toEndo, v, hv
    intro x
    have : ⁅x, v⁆ = ⁅toEndo x, v⁆ := rfl
    rw [LinearMap.comp_apply, this, hχ' (toEndo x)]
  have hsolv : IsSolvable k imL := LieHom.isSolvable_range (LieModule.toEnd k L V)
  apply LieModule.exists_forall_lie_eq_smul_finrank (L := imL)
