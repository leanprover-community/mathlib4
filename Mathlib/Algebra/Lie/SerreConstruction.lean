/-
Copyright (c) 2021 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash, Jonathan Reich
-/
module

public import Mathlib.Algebra.Lie.Free
public import Mathlib.Algebra.Lie.Quotient
public import Mathlib.Data.Matrix.Cartan

/-!
# Serre construction of Lie algebras from Cartan matrices

This file provides the Serre construction of Lie algebras from Cartan matrices.
Given a Cartan matrix `A`, we construct a Lie algebra as a quotient of the free Lie algebra
on generators `{H_i, E_i, F_i}` by the Serre relations.

## Main definitions

* `Matrix.ToLieAlgebra` : The Lie algebra constructed from a Cartan matrix via Serre relations

### Exceptional Lie algebras
* `LieAlgebra.e₆`, `LieAlgebra.e₇`, `LieAlgebra.e₈`, `LieAlgebra.f₄`, `LieAlgebra.g₂`

### Classical Lie algebras
* `CartanMatrix.aₙ`, `CartanMatrix.bₙ`, `CartanMatrix.cₙ`, `CartanMatrix.dₙ`

## References

* [N. Bourbaki, *Lie Groups and Lie Algebras, Chapters 4--6*](bourbaki1968)
* [N. Bourbaki, *Lie Groups and Lie Algebras, Chapters 7--9*](bourbaki1975b) chapter VIII, §4.3
* [J.P. Serre, *Complex Semisimple Lie Algebras*](serre1965) chapter VI, appendix

## Tags

lie algebra, semi-simple, cartan matrix, serre relations
-/

@[expose] public section

universe u v w

noncomputable section

variable (R : Type u) {B : Type v} [CommRing R]
variable (CM : Matrix B B ℤ)

namespace CartanMatrix

variable (B)

/-- The generators of the free Lie algebra from which we construct the Lie algebra of a Cartan
matrix as a quotient. -/
inductive Generators
  | H : B → Generators
  | E : B → Generators
  | F : B → Generators

instance [Inhabited B] : Inhabited (Generators B) :=
  ⟨Generators.H default⟩

variable {B}

namespace Relations

open Function

local notation "H" => FreeLieAlgebra.of R ∘ Generators.H
local notation "E" => FreeLieAlgebra.of R ∘ Generators.E
local notation "F" => FreeLieAlgebra.of R ∘ Generators.F
local notation "ad" => LieAlgebra.ad R (FreeLieAlgebra R (Generators B))

/-- The terms corresponding to the `⁅H, H⁆`-relations. -/
def HH : B × B → FreeLieAlgebra R (Generators B) :=
  uncurry fun i j => ⁅H i, H j⁆

/-- The terms corresponding to the `⁅E, F⁆`-relations. -/
def EF [DecidableEq B] : B × B → FreeLieAlgebra R (Generators B) :=
  uncurry fun i j => if i = j then ⁅E i, F i⁆ - H i else ⁅E i, F j⁆

/-- The terms corresponding to the `⁅H, E⁆`-relations. -/
def HE : B × B → FreeLieAlgebra R (Generators B) :=
  uncurry fun i j => ⁅H i, E j⁆ - CM i j • E j

/-- The terms corresponding to the `⁅H, F⁆`-relations. -/
def HF : B × B → FreeLieAlgebra R (Generators B) :=
  uncurry fun i j => ⁅H i, F j⁆ + CM i j • F j

/-- The terms corresponding to the `ad E`-relations. -/
def adE : B × B → FreeLieAlgebra R (Generators B) :=
  uncurry fun i j => ad (E i) ^ (-CM i j).toNat <| ⁅E i, E j⁆

/-- The terms corresponding to the `ad F`-relations. -/
def adF : B × B → FreeLieAlgebra R (Generators B) :=
  uncurry fun i j => ad (F i) ^ (-CM i j).toNat <| ⁅F i, F j⁆

private theorem adE_of_eq_eq_zero (i : B) (h : CM i i = 2) : adE R CM ⟨i, i⟩ = 0 := by
  have h' : (-2 : ℤ).toNat = 0 := rfl
  simp [adE, h, h']

private theorem adF_of_eq_eq_zero (i : B) (h : CM i i = 2) : adF R CM ⟨i, i⟩ = 0 := by
  have h' : (-2 : ℤ).toNat = 0 := rfl
  simp [adF, h, h']

/-- The union of all the relations as a subset of the free Lie algebra. -/
def toSet [DecidableEq B] : Set (FreeLieAlgebra R (Generators B)) :=
  (Set.range <| HH R) ∪ (Set.range <| EF R) ∪ (Set.range <| HE R CM) ∪ (Set.range <| HF R CM) ∪
    (Set.range <| adE R CM) ∪
    (Set.range <| adF R CM)

/-- The ideal of the free Lie algebra generated by the relations. -/
def toIdeal [DecidableEq B] : LieIdeal R (FreeLieAlgebra R (Generators B)) :=
  LieSubmodule.lieSpan R _ <| toSet R CM

end Relations

end CartanMatrix

variable [DecidableEq B]

/-- The Lie algebra corresponding to a Cartan matrix.

Note that it is defined for any matrix of integers. Its value for non-Cartan matrices should be
regarded as junk. -/
def Matrix.ToLieAlgebra :=
  FreeLieAlgebra R _ ⧸ CartanMatrix.Relations.toIdeal R CM
deriving LieRing, Inhabited, LieAlgebra R

namespace LieAlgebra

/-- The exceptional split Lie algebra of type e₆. -/
abbrev e₆ :=
  CartanMatrix.E₆.ToLieAlgebra R

/-- The exceptional split Lie algebra of type e₇. -/
abbrev e₇ :=
  CartanMatrix.E₇.ToLieAlgebra R

/-- The exceptional split Lie algebra of type e₈. -/
abbrev e₈ :=
  CartanMatrix.E₈.ToLieAlgebra R

/-- The exceptional split Lie algebra of type f₄. -/
abbrev f₄ :=
  CartanMatrix.F₄.ToLieAlgebra R

/-- The exceptional split Lie algebra of type g₂. -/
abbrev g₂ :=
  CartanMatrix.G₂.ToLieAlgebra R

end LieAlgebra

end

namespace CartanMatrix

variable (R : Type*) [CommRing R]

/-- The Lie algebra of type Aₙ₋₁, isomorphic to sl(n). -/
noncomputable abbrev aₙ (n : ℕ) [NeZero n] :=
  (A n).ToLieAlgebra R

/-- The Lie algebra of type Bₙ, isomorphic to so(2n+1). -/
noncomputable abbrev bₙ (n : ℕ) [NeZero n] :=
  (B n).ToLieAlgebra R

/-- The Lie algebra of type Cₙ, isomorphic to sp(2n). -/
noncomputable abbrev cₙ (n : ℕ) [NeZero n] :=
  (C n).ToLieAlgebra R

/-- The Lie algebra of type Dₙ, isomorphic to so(2n). Requires n ≥ 4 for non-degenerate behavior. -/
noncomputable abbrev dₙ (n : ℕ) [NeZero n] :=
  (D n).ToLieAlgebra R

end CartanMatrix
