/-
Copyright (c) 2021 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.Algebra.Ring.Divisibility.Lemmas
import Mathlib.Algebra.Lie.Nilpotent
import Mathlib.Algebra.Lie.TensorProduct
import Mathlib.Algebra.Lie.Engel
import Mathlib.LinearAlgebra.Eigenspace.Triangularizable
import Mathlib.LinearAlgebra.TensorProduct.Tower
import Mathlib.RingTheory.Artinian

#align_import algebra.lie.weights from "leanprover-community/mathlib"@"6b0169218d01f2837d79ea2784882009a0da1aa1"

/-!
# Weight spaces of Lie modules of nilpotent Lie algebras

Just as a key tool when studying the behaviour of a linear operator is to decompose the space on
which it acts into a sum of (generalised) eigenspaces, a key tool when studying a representation `M`
of Lie algebra `L` is to decompose `M` into a sum of simultaneous eigenspaces of `x` as `x` ranges
over `L`. These simultaneous generalised eigenspaces are known as the weight spaces of `M`.

When `L` is nilpotent, it follows from the binomial theorem that weight spaces are Lie submodules.

Basic definitions and properties of the above ideas are provided in this file.

## Main definitions

  * `LieModule.weightSpaceOf`
  * `LieModule.weightSpace`
  * `LieModule.posFittingCompOf`
  * `LieModule.posFittingComp`
  * `LieModule.iSup_ucs_eq_weightSpace_zero`
  * `LieModule.iInf_lowerCentralSeries_eq_posFittingComp`
  * `LieModule.isCompl_weightSpace_zero_posFittingComp`
  * `LieModule.independent_weightSpace`
  * `LieModule.iSup_weightSpace_eq_top`

## References

* [N. Bourbaki, *Lie Groups and Lie Algebras, Chapters 7--9*](bourbaki1975b)

## Tags

lie character, eigenvalue, eigenspace, weight, weight vector, root, root vector
-/

variable {K R L M : Type*} [CommRing R] [LieRing L] [LieAlgebra R L] [LieAlgebra.IsNilpotent R L]
  [AddCommGroup M] [Module R M] [LieRingModule L M] [LieModule R L M]

namespace LieModule

open Set Function LieAlgebra TensorProduct TensorProduct.LieModule
open scoped BigOperators TensorProduct

section notation_weight_space_of

/-- Until we define `LieModule.weightSpaceOf`, it is useful to have some notation as follows: -/
local notation3 "ùïé("M", " œá", " x")" => (toEndomorphism R L M x).maximalGeneralizedEigenspace œá

/-- See also `bourbaki1975b` Chapter VII ¬ß1.1, Proposition 2 (ii). -/
protected theorem weight_vector_multiplication (M‚ÇÅ M‚ÇÇ M‚ÇÉ : Type*)
    [AddCommGroup M‚ÇÅ] [Module R M‚ÇÅ] [LieRingModule L M‚ÇÅ] [LieModule R L M‚ÇÅ] [AddCommGroup M‚ÇÇ]
    [Module R M‚ÇÇ] [LieRingModule L M‚ÇÇ] [LieModule R L M‚ÇÇ] [AddCommGroup M‚ÇÉ] [Module R M‚ÇÉ]
    [LieRingModule L M‚ÇÉ] [LieModule R L M‚ÇÉ] (g : M‚ÇÅ ‚äó[R] M‚ÇÇ ‚Üí‚Çó‚ÅÖR,L‚ÅÜ M‚ÇÉ) (œá‚ÇÅ œá‚ÇÇ : R) (x : L) :
    LinearMap.range ((g : M‚ÇÅ ‚äó[R] M‚ÇÇ ‚Üí‚Çó[R] M‚ÇÉ).comp (mapIncl ùïé(M‚ÇÅ, œá‚ÇÅ, x) ùïé(M‚ÇÇ, œá‚ÇÇ, x))) ‚â§
      ùïé(M‚ÇÉ, œá‚ÇÅ + œá‚ÇÇ, x) := by
  -- Unpack the statement of the goal.
  intro m‚ÇÉ
  simp only [TensorProduct.mapIncl, LinearMap.mem_range, LinearMap.coe_comp,
    LieModuleHom.coe_toLinearMap, Function.comp_apply, Pi.add_apply, exists_imp,
    Module.End.mem_maximalGeneralizedEigenspace]
  rintro t rfl
  -- Set up some notation.
  let F : Module.End R M‚ÇÉ := toEndomorphism R L M‚ÇÉ x - (œá‚ÇÅ + œá‚ÇÇ) ‚Ä¢ ‚Üë1
  -- The goal is linear in `t` so use induction to reduce to the case that `t` is a pure tensor.
  refine t.induction_on ?_ ?_ ?_
  ¬∑ use 0; simp only [LinearMap.map_zero, LieModuleHom.map_zero]
  swap
  ¬∑ rintro t‚ÇÅ t‚ÇÇ ‚ü®k‚ÇÅ, hk‚ÇÅ‚ü© ‚ü®k‚ÇÇ, hk‚ÇÇ‚ü©; use max k‚ÇÅ k‚ÇÇ
    simp only [LieModuleHom.map_add, LinearMap.map_add,
      LinearMap.pow_map_zero_of_le (le_max_left k‚ÇÅ k‚ÇÇ) hk‚ÇÅ,
      LinearMap.pow_map_zero_of_le (le_max_right k‚ÇÅ k‚ÇÇ) hk‚ÇÇ, add_zero]
  -- Now the main argument: pure tensors.
  rintro ‚ü®m‚ÇÅ, hm‚ÇÅ‚ü© ‚ü®m‚ÇÇ, hm‚ÇÇ‚ü©
  change ‚àÉ k, (F ^ k) ((g : M‚ÇÅ ‚äó[R] M‚ÇÇ ‚Üí‚Çó[R] M‚ÇÉ) (m‚ÇÅ ‚äó‚Çú m‚ÇÇ)) = (0 : M‚ÇÉ)
  -- Eliminate `g` from the picture.
  let f‚ÇÅ : Module.End R (M‚ÇÅ ‚äó[R] M‚ÇÇ) := (toEndomorphism R L M‚ÇÅ x - œá‚ÇÅ ‚Ä¢ ‚Üë1).rTensor M‚ÇÇ
  let f‚ÇÇ : Module.End R (M‚ÇÅ ‚äó[R] M‚ÇÇ) := (toEndomorphism R L M‚ÇÇ x - œá‚ÇÇ ‚Ä¢ ‚Üë1).lTensor M‚ÇÅ
  have h_comm_square : F ‚àò‚Çó ‚Üëg = (g : M‚ÇÅ ‚äó[R] M‚ÇÇ ‚Üí‚Çó[R] M‚ÇÉ).comp (f‚ÇÅ + f‚ÇÇ) := by
    ext m‚ÇÅ m‚ÇÇ;
    simp only [‚Üê g.map_lie x (m‚ÇÅ ‚äó‚Çú m‚ÇÇ), add_smul, sub_tmul, tmul_sub, smul_tmul, lie_tmul_right,
      tmul_smul, toEndomorphism_apply_apply, LieModuleHom.map_smul, LinearMap.one_apply,
      LieModuleHom.coe_toLinearMap, LinearMap.smul_apply, Function.comp_apply, LinearMap.coe_comp,
      LinearMap.rTensor_tmul, LieModuleHom.map_add, LinearMap.add_apply, LieModuleHom.map_sub,
      LinearMap.sub_apply, LinearMap.lTensor_tmul, AlgebraTensorModule.curry_apply,
      TensorProduct.curry_apply, LinearMap.toFun_eq_coe, LinearMap.coe_restrictScalars]
    abel
  rsuffices ‚ü®k, hk‚ü© : ‚àÉ k : ‚Ñï, ((f‚ÇÅ + f‚ÇÇ) ^ k) (m‚ÇÅ ‚äó‚Çú m‚ÇÇ) = 0
  ¬∑ use k
    change (F ^ k) (g.toLinearMap (m‚ÇÅ ‚äó‚Çú[R] m‚ÇÇ)) = 0
    rw [‚Üê LinearMap.comp_apply, LinearMap.commute_pow_left_of_commute h_comm_square,
      LinearMap.comp_apply, hk, LinearMap.map_zero]
  -- Unpack the information we have about `m‚ÇÅ`, `m‚ÇÇ`.
  simp only [Module.End.mem_maximalGeneralizedEigenspace] at hm‚ÇÅ hm‚ÇÇ
  obtain ‚ü®k‚ÇÅ, hk‚ÇÅ‚ü© := hm‚ÇÅ
  obtain ‚ü®k‚ÇÇ, hk‚ÇÇ‚ü© := hm‚ÇÇ
  have hf‚ÇÅ : (f‚ÇÅ ^ k‚ÇÅ) (m‚ÇÅ ‚äó‚Çú m‚ÇÇ) = 0 := by
    simp only [hk‚ÇÅ, zero_tmul, LinearMap.rTensor_tmul, LinearMap.rTensor_pow]
  have hf‚ÇÇ : (f‚ÇÇ ^ k‚ÇÇ) (m‚ÇÅ ‚äó‚Çú m‚ÇÇ) = 0 := by
    simp only [hk‚ÇÇ, tmul_zero, LinearMap.lTensor_tmul, LinearMap.lTensor_pow]
  -- It's now just an application of the binomial theorem.
  use k‚ÇÅ + k‚ÇÇ - 1
  have hf_comm : Commute f‚ÇÅ f‚ÇÇ := by
    ext m‚ÇÅ m‚ÇÇ
    simp only [LinearMap.mul_apply, LinearMap.rTensor_tmul, LinearMap.lTensor_tmul,
      AlgebraTensorModule.curry_apply, LinearMap.toFun_eq_coe, LinearMap.lTensor_tmul,
      TensorProduct.curry_apply, LinearMap.coe_restrictScalars]
  rw [hf_comm.add_pow']
  simp only [TensorProduct.mapIncl, Submodule.subtype_apply, Finset.sum_apply, Submodule.coe_mk,
    LinearMap.coeFn_sum, TensorProduct.map_tmul, LinearMap.smul_apply]
  -- The required sum is zero because each individual term is zero.
  apply Finset.sum_eq_zero
  rintro ‚ü®i, j‚ü© hij
  -- Eliminate the binomial coefficients from the picture.
  suffices (f‚ÇÅ ^ i * f‚ÇÇ ^ j) (m‚ÇÅ ‚äó‚Çú m‚ÇÇ) = 0 by rw [this]; apply smul_zero
  -- Finish off with appropriate case analysis.
  cases' Nat.le_or_le_of_add_eq_add_pred (Finset.mem_antidiagonal.mp hij) with hi hj
  ¬∑ rw [(hf_comm.pow_pow i j).eq, LinearMap.mul_apply, LinearMap.pow_map_zero_of_le hi hf‚ÇÅ,
      LinearMap.map_zero]
  ¬∑ rw [LinearMap.mul_apply, LinearMap.pow_map_zero_of_le hj hf‚ÇÇ, LinearMap.map_zero]

lemma lie_mem_maxGenEigenspace_toEndomorphism
    {œá‚ÇÅ œá‚ÇÇ : R} {x y : L} {m : M} (hy : y ‚àà ùïé(L, œá‚ÇÅ, x)) (hm : m ‚àà ùïé(M, œá‚ÇÇ, x)) :
    ‚ÅÖy, m‚ÅÜ ‚àà ùïé(M, œá‚ÇÅ + œá‚ÇÇ, x) := by
  apply LieModule.weight_vector_multiplication L M M (toModuleHom R L M) œá‚ÇÅ œá‚ÇÇ
  simp only [LieModuleHom.coe_toLinearMap, Function.comp_apply, LinearMap.coe_comp,
    TensorProduct.mapIncl, LinearMap.mem_range]
  use ‚ü®y, hy‚ü© ‚äó‚Çú ‚ü®m, hm‚ü©
  simp only [Submodule.subtype_apply, toModuleHom_apply, TensorProduct.map_tmul]

variable (M)

/-- If `M` is a representation of a nilpotent Lie algebra `L`, `œá` is a scalar, and `x : L`, then
`weightSpaceOf M œá x` is the maximal generalized `œá`-eigenspace of the action of `x` on `M`.

It is a Lie submodule because `L` is nilpotent. -/
def weightSpaceOf (œá : R) (x : L) : LieSubmodule R L M :=
  { ùïé(M, œá, x) with
    lie_mem := by
      intro y m hm
      simp only [AddSubsemigroup.mem_carrier, AddSubmonoid.mem_toSubsemigroup,
        Submodule.mem_toAddSubmonoid] at hm ‚ä¢
      rw [‚Üê zero_add œá]
      exact lie_mem_maxGenEigenspace_toEndomorphism (by simp) hm }

end notation_weight_space_of

variable (M)

theorem mem_weightSpaceOf (œá : R) (x : L) (m : M) :
    m ‚àà weightSpaceOf M œá x ‚Üî ‚àÉ k : ‚Ñï, ((toEndomorphism R L M x - œá ‚Ä¢ ‚Üë1) ^ k) m = 0 := by
  simp [weightSpaceOf]

theorem coe_weightSpaceOf_zero (x : L) :
    ‚Üë(weightSpaceOf M (0 : R) x) = ‚®Ü k, LinearMap.ker (toEndomorphism R L M x ^ k) := by
  simp [weightSpaceOf, Module.End.maximalGeneralizedEigenspace]

/-- If `M` is a representation of a nilpotent Lie algebra `L` and `œá : L ‚Üí R` is a family of
scalars, then `weightSpace M œá` is the intersection of the maximal generalized `œá x`-eigenspaces of
the action of `x` on `M` as `x` ranges over `L`.

It is a Lie submodule because `L` is nilpotent. -/
def weightSpace (œá : L ‚Üí R) : LieSubmodule R L M :=
  ‚®Ö x, weightSpaceOf M (œá x) x

theorem mem_weightSpace (œá : L ‚Üí R) (m : M) :
    m ‚àà weightSpace M œá ‚Üî ‚àÄ x, ‚àÉ k : ‚Ñï, ((toEndomorphism R L M x - œá x ‚Ä¢ ‚Üë1) ^ k) m = 0 := by
  simp [weightSpace, mem_weightSpaceOf]

lemma weightSpace_le_weightSpaceOf (x : L) (œá : L ‚Üí R) :
    weightSpace M œá ‚â§ weightSpaceOf M (œá x) x :=
  iInf_le _ x

/-- See also the more useful form `LieModule.zero_weightSpace_eq_top_of_nilpotent`. -/
@[simp]
theorem zero_weightSpace_eq_top_of_nilpotent' [IsNilpotent R L M] :
    weightSpace M (0 : L ‚Üí R) = ‚ä§ := by
  ext
  simp [weightSpace, weightSpaceOf]
#align lie_module.zero_weight_space_eq_top_of_nilpotent' LieModule.zero_weightSpace_eq_top_of_nilpotent'

theorem coe_weightSpace_of_top (œá : L ‚Üí R) :
    (weightSpace M (œá ‚àò (‚ä§ : LieSubalgebra R L).incl) : Submodule R M) = weightSpace M œá := by
  ext m
  simp only [mem_weightSpace, LieSubmodule.mem_coeSubmodule, Subtype.forall]
  apply forall_congr'
  simp
#align lie_module.coe_weight_space_of_top LieModule.coe_weightSpace_of_top

@[simp]
theorem zero_weightSpace_eq_top_of_nilpotent [IsNilpotent R L M] :
    weightSpace M (0 : (‚ä§ : LieSubalgebra R L) ‚Üí R) = ‚ä§ := by
  ext m
  simp only [mem_weightSpace, Pi.zero_apply, zero_smul, sub_zero, Subtype.forall, forall_true_left,
    LieSubalgebra.toEndomorphism_mk, LieSubalgebra.mem_top, LieSubmodule.mem_top, iff_true]
  intro x
  obtain ‚ü®k, hk‚ü© := exists_forall_pow_toEndomorphism_eq_zero R L M
  exact ‚ü®k, by simp [hk x]‚ü©
#align lie_module.zero_weight_space_eq_top_of_nilpotent LieModule.zero_weightSpace_eq_top_of_nilpotent

theorem exists_weightSpace_le_ker_of_isNoetherian [IsNoetherian R M] (œá : L ‚Üí R) (x : L) :
    ‚àÉ k : ‚Ñï,
      weightSpace M œá ‚â§ LinearMap.ker ((toEndomorphism R L M x - algebraMap R _ (œá x)) ^ k) := by
  use (toEndomorphism R L M x).maximalGeneralizedEigenspaceIndex (œá x)
  intro m hm
  replace hm : m ‚àà (toEndomorphism R L M x).maximalGeneralizedEigenspace (œá x) :=
    weightSpace_le_weightSpaceOf M x œá hm
  rwa [Module.End.maximalGeneralizedEigenspace_eq] at hm

variable (R) in
theorem exists_weightSpace_zero_le_ker_of_isNoetherian
    [IsNoetherian R M] (x : L) :
    ‚àÉ k : ‚Ñï, weightSpace M (0 : L ‚Üí R) ‚â§ LinearMap.ker (toEndomorphism R L M x ^ k) := by
  simpa using exists_weightSpace_le_ker_of_isNoetherian M (0 : L ‚Üí R) x

lemma isNilpotent_toEndomorphism_sub_algebraMap [IsNoetherian R M] (œá : L ‚Üí R) (x : L) :
    _root_.IsNilpotent <| toEndomorphism R L (weightSpace M œá) x - algebraMap R _ (œá x) := by
  have : toEndomorphism R L (weightSpace M œá) x - algebraMap R _ (œá x) =
      (toEndomorphism R L M x - algebraMap R _ (œá x)).restrict
        (fun m hm ‚Ü¶ sub_mem (LieSubmodule.lie_mem _ hm) (Submodule.smul_mem _ _ hm)) := by
    rfl
  obtain ‚ü®k, hk‚ü© := exists_weightSpace_le_ker_of_isNoetherian M œá x
  use k
  ext ‚ü®m, hm‚ü©
  simpa [this, LinearMap.pow_restrict _, LinearMap.restrict_apply] using hk hm

/-- A (nilpotent) Lie algebra acts nilpotently on the zero weight space of a Noetherian Lie
module. -/
theorem isNilpotent_toEndomorphism_weightSpace_zero [IsNoetherian R M] (x : L) :
    _root_.IsNilpotent <| toEndomorphism R L (weightSpace M (0 : L ‚Üí R)) x := by
  simpa using isNilpotent_toEndomorphism_sub_algebraMap M (0 : L ‚Üí R) x
#align lie_module.is_nilpotent_to_endomorphism_weight_space_zero LieModule.isNilpotent_toEndomorphism_weightSpace_zero

/-- By Engel's theorem, the zero weight space of a Noetherian Lie module is nilpotent. -/
instance [IsNoetherian R M] :
    IsNilpotent R L (weightSpace M (0 : L ‚Üí R)) :=
  isNilpotent_iff_forall'.mpr <| isNilpotent_toEndomorphism_weightSpace_zero M

variable (R L)

@[simp]
lemma weightSpace_zero_normalizer_eq_self :
    (weightSpace M (0 : L ‚Üí R)).normalizer = weightSpace M 0 := by
  refine' le_antisymm _ (LieSubmodule.le_normalizer _)
  intro m hm
  rw [LieSubmodule.mem_normalizer] at hm
  simp only [mem_weightSpace, Pi.zero_apply, zero_smul, sub_zero] at hm ‚ä¢
  intro y
  obtain ‚ü®k, hk‚ü© := hm y y
  use k + 1
  simpa [pow_succ', LinearMap.mul_eq_comp]

lemma iSup_ucs_le_weightSpace_zero :
    ‚®Ü k, (‚ä• : LieSubmodule R L M).ucs k ‚â§ weightSpace M (0 : L ‚Üí R) := by
  simpa using LieSubmodule.ucs_le_of_normalizer_eq_self (weightSpace_zero_normalizer_eq_self R L M)

/-- See also `LieModule.iInf_lowerCentralSeries_eq_posFittingComp`. -/
lemma iSup_ucs_eq_weightSpace_zero [IsNoetherian R M] :
    ‚®Ü k, (‚ä• : LieSubmodule R L M).ucs k = weightSpace M (0 : L ‚Üí R) := by
  obtain ‚ü®k, hk‚ü© := (LieSubmodule.isNilpotent_iff_exists_self_le_ucs
    <| weightSpace M (0 : L ‚Üí R)).mp inferInstance
  refine le_antisymm (iSup_ucs_le_weightSpace_zero R L M) (le_trans hk ?_)
  exact le_iSup (fun k ‚Ü¶ (‚ä• : LieSubmodule R L M).ucs k) k

variable {L}

/-- If `M` is a representation of a nilpotent Lie algebra `L`, and `x : L`, then
`posFittingCompOf R M x` is the infimum of the decreasing system
`range œÜ‚Çì ‚äá range œÜ‚Çì¬≤ ‚äá range œÜ‚Çì¬≥ ‚äá ‚ãØ` where `œÜ‚Çì : End R M := toEndomorphism R L M x`. We call this
the "positive Fitting component" because with appropriate assumptions (e.g., `R` is a field and
`M` is finite-dimensional) `œÜ‚Çì` induces the so-called Fitting decomposition: `M = M‚ÇÄ ‚äï M‚ÇÅ` where
`M‚ÇÄ = weightSpaceOf M 0 x` and `M‚ÇÅ = posFittingCompOf R M x`.

It is a Lie submodule because `L` is nilpotent. -/
def posFittingCompOf (x : L) : LieSubmodule R L M :=
  { toSubmodule := ‚®Ö k, LinearMap.range (toEndomorphism R L M x ^ k)
    lie_mem := by
      set œÜ := toEndomorphism R L M x
      intros y m hm
      simp only [AddSubsemigroup.mem_carrier, AddSubmonoid.mem_toSubsemigroup,
        Submodule.mem_toAddSubmonoid, Submodule.mem_iInf, LinearMap.mem_range] at hm ‚ä¢
      intro k
      obtain ‚ü®N, hN‚ü© := LieAlgebra.nilpotent_ad_of_nilpotent_algebra R L
      obtain ‚ü®m, rfl‚ü© := hm (N + k)
      let f‚ÇÅ : Module.End R (L ‚äó[R] M) := (LieAlgebra.ad R L x).rTensor M
      let f‚ÇÇ : Module.End R (L ‚äó[R] M) := œÜ.lTensor L
      replace hN : f‚ÇÅ ^ N = 0 := by ext; simp [hN]
      have h‚ÇÅ : Commute f‚ÇÅ f‚ÇÇ := by ext; simp
      have h‚ÇÇ : œÜ ‚àò‚Çó toModuleHom R L M = toModuleHom R L M ‚àò‚Çó (f‚ÇÅ + f‚ÇÇ) := by ext; simp
      obtain ‚ü®q, hq‚ü© := h‚ÇÅ.add_pow_dvd_pow_of_pow_eq_zero_right (N + k).le_succ hN
      use toModuleHom R L M (q (y ‚äó‚Çú m))
      change (œÜ ^ k).comp ((toModuleHom R L M : L ‚äó[R] M ‚Üí‚Çó[R] M)) _ = _
      simp [LinearMap.commute_pow_left_of_commute h‚ÇÇ, LinearMap.comp_apply (g := (f‚ÇÅ + f‚ÇÇ) ^ k),
        ‚Üê LinearMap.comp_apply (g := q), ‚Üê LinearMap.mul_eq_comp, ‚Üê hq] }

variable {M} in
lemma mem_posFittingCompOf (x : L) (m : M) :
    m ‚àà posFittingCompOf R M x ‚Üî ‚àÄ (k : ‚Ñï), ‚àÉ n, (toEndomorphism R L M x ^ k) n = m := by
  simp [posFittingCompOf]

@[simp] lemma posFittingCompOf_le_lowerCentralSeries (x : L) (k : ‚Ñï) :
    posFittingCompOf R M x ‚â§ lowerCentralSeries R L M k := by
  suffices : ‚àÄ m l, (toEndomorphism R L M x ^ l) m ‚àà lowerCentralSeries R L M l
  ¬∑ intro m hm
    obtain ‚ü®n, rfl‚ü© := (mem_posFittingCompOf R x m).mp hm k
    exact this n k
  intro m l
  induction' l with l ih; simp
  simp only [lowerCentralSeries_succ, pow_succ, LinearMap.mul_apply]
  exact LieSubmodule.lie_mem_lie _ ‚ä§ (LieSubmodule.mem_top x) ih

@[simp] lemma posFittingCompOf_eq_bot_of_isNilpotent
    [IsNilpotent R L M] (x : L) :
    posFittingCompOf R M x = ‚ä• := by
  simp_rw [eq_bot_iff, ‚Üê iInf_lowerCentralSeries_eq_bot_of_isNilpotent, le_iInf_iff,
    posFittingCompOf_le_lowerCentralSeries, forall_const]

variable (L)

/-- If `M` is a representation of a nilpotent Lie algebra `L` with coefficients in `R`, then
`posFittingComp R L M` is the span of the positive Fitting components of the action of `x` on `M`,
as `x` ranges over `L`.

It is a Lie submodule because `L` is nilpotent. -/
def posFittingComp : LieSubmodule R L M :=
  ‚®Ü x, posFittingCompOf R M x

lemma mem_posFittingComp (m : M) :
    m ‚àà posFittingComp R L M ‚Üî m ‚àà ‚®Ü (x : L), posFittingCompOf R M x := by
  rfl

lemma posFittingCompOf_le_posFittingComp (x : L) :
    posFittingCompOf R M x ‚â§ posFittingComp R L M := by
  rw [posFittingComp]; exact le_iSup (posFittingCompOf R M) x

lemma posFittingComp_le_iInf_lowerCentralSeries :
    posFittingComp R L M ‚â§ ‚®Ö k, lowerCentralSeries R L M k := by
  simp [posFittingComp]

/-- See also `LieModule.iSup_ucs_eq_weightSpace_zero`. -/
@[simp] lemma iInf_lowerCentralSeries_eq_posFittingComp
    [IsNoetherian R M] [IsArtinian R M] :
    ‚®Ö k, lowerCentralSeries R L M k = posFittingComp R L M := by
  refine le_antisymm ?_ (posFittingComp_le_iInf_lowerCentralSeries R L M)
  apply iInf_lcs_le_of_isNilpotent_quot
  rw [LieModule.isNilpotent_iff_forall']
  intro x
  obtain ‚ü®k, hk‚ü© := Filter.eventually_atTop.mp (toEndomorphism R L M x).eventually_iInf_range_pow_eq
  use k
  ext ‚ü®m‚ü©
  set F := posFittingComp R L M
  replace hk : (toEndomorphism R L M x ^ k) m ‚àà F := by
    apply posFittingCompOf_le_posFittingComp R L M x
    simp_rw [‚Üê LieSubmodule.mem_coeSubmodule, posFittingCompOf, hk k (le_refl k)]
    apply LinearMap.mem_range_self
  suffices (toEndomorphism R L (M ‚ß∏ F) x ^ k) (LieSubmodule.Quotient.mk (N := F) m) =
    LieSubmodule.Quotient.mk (N := F) ((toEndomorphism R L M x ^ k) m) by simpa [this]
  have := LinearMap.congr_fun (LinearMap.commute_pow_left_of_commute
    (LieSubmodule.Quotient.toEndomorphism_comp_mk' F x) k) m
  simpa using this

@[simp] lemma posFittingComp_eq_bot_of_isNilpotent
    [IsNilpotent R L M] :
    posFittingComp R L M = ‚ä• := by
  simp [posFittingComp]

section map_comap

variable {R L M}
variable
  {M‚ÇÇ : Type*} [AddCommGroup M‚ÇÇ] [Module R M‚ÇÇ] [LieRingModule L M‚ÇÇ] [LieModule R L M‚ÇÇ]
  {œá : L ‚Üí R} (f : M ‚Üí‚Çó‚ÅÖR,L‚ÅÜ M‚ÇÇ)

lemma map_posFittingComp_le :
    (posFittingComp R L M).map f ‚â§ posFittingComp R L M‚ÇÇ := by
  rw [posFittingComp, posFittingComp, LieSubmodule.map_iSup]
  refine iSup_mono fun y ‚Ü¶ LieSubmodule.map_le_iff_le_comap.mpr fun m hm ‚Ü¶ ?_
  simp only [mem_posFittingCompOf] at hm
  simp only [LieSubmodule.mem_comap, mem_posFittingCompOf]
  intro k
  obtain ‚ü®n, hn‚ü© := hm k
  use f n
  rw [LieModule.toEndomorphism_pow_apply_map, hn]

lemma map_weightSpace_le :
    (weightSpace M œá).map f ‚â§ weightSpace M‚ÇÇ œá := by
  rw [LieSubmodule.map_le_iff_le_comap]
  intro m hm
  simp only [LieSubmodule.mem_comap, mem_weightSpace]
  intro x
  have : (toEndomorphism R L M‚ÇÇ x - œá x ‚Ä¢ ‚Üë1) ‚àò‚Çó f = f ‚àò‚Çó (toEndomorphism R L M x - œá x ‚Ä¢ ‚Üë1) := by
    ext; simp
  obtain ‚ü®k, h‚ü© := (mem_weightSpace _ _ _).mp hm x
  exact ‚ü®k, by simpa [h] using LinearMap.congr_fun (LinearMap.commute_pow_left_of_commute this k) m‚ü©

variable {f}

lemma comap_weightSpace_eq_of_injective (hf : Injective f) :
    (weightSpace M‚ÇÇ œá).comap f = weightSpace M œá := by
  refine le_antisymm (fun m hm ‚Ü¶ ?_) ?_
  ¬∑ simp only [LieSubmodule.mem_comap, mem_weightSpace] at hm
    simp only [mem_weightSpace]
    intro x
    have h : (toEndomorphism R L M‚ÇÇ x - œá x ‚Ä¢ ‚Üë1) ‚àò‚Çó f =
             f ‚àò‚Çó (toEndomorphism R L M x - œá x ‚Ä¢ ‚Üë1) := by ext; simp
    obtain ‚ü®k, hk‚ü© := hm x
    use k
    suffices : f (((toEndomorphism R L M x - œá x ‚Ä¢ ‚Üë1) ^ k) m) = 0
    ¬∑ rw [‚Üê f.map_zero] at this; exact hf this
    simpa [hk] using (LinearMap.congr_fun (LinearMap.commute_pow_left_of_commute h k) m).symm
  ¬∑ rw [‚Üê LieSubmodule.map_le_iff_le_comap]
    exact map_weightSpace_le f

lemma map_weightSpace_eq_of_injective (hf : Injective f) :
    (weightSpace M œá).map f = weightSpace M‚ÇÇ œá ‚äì f.range := by
  refine le_antisymm (le_inf_iff.mpr ‚ü®map_weightSpace_le f, LieSubmodule.map_le_range f‚ü©) ?_
  rintro - ‚ü®hm, ‚ü®m, rfl‚ü©‚ü©
  simp only [‚Üê comap_weightSpace_eq_of_injective hf, LieSubmodule.mem_map, LieSubmodule.mem_comap]
  exact ‚ü®m, hm, rfl‚ü©

lemma map_weightSpace_eq (e : M ‚âÉ‚Çó‚ÅÖR,L‚ÅÜ M‚ÇÇ) :
    (weightSpace M œá).map e = weightSpace M‚ÇÇ œá := by
  simp [map_weightSpace_eq_of_injective e.injective]

lemma map_posFittingComp_eq (e : M ‚âÉ‚Çó‚ÅÖR,L‚ÅÜ M‚ÇÇ) :
    (posFittingComp R L M).map e = posFittingComp R L M‚ÇÇ := by
  refine le_antisymm (map_posFittingComp_le _) ?_
  suffices posFittingComp R L M‚ÇÇ = ((posFittingComp R L M‚ÇÇ).map (e.symm : M‚ÇÇ ‚Üí‚Çó‚ÅÖR,L‚ÅÜ M)).map e by
    rw [this]
    exact LieSubmodule.map_mono (map_posFittingComp_le _)
  rw [‚Üê LieSubmodule.map_comp]
  convert LieSubmodule.map_id
  ext
  simp

lemma posFittingComp_map_incl_sup_of_codisjoint [IsNoetherian R M] [IsArtinian R M]
    {N‚ÇÅ N‚ÇÇ : LieSubmodule R L M} (h : Codisjoint N‚ÇÅ N‚ÇÇ) :
    (posFittingComp R L N‚ÇÅ).map N‚ÇÅ.incl ‚äî (posFittingComp R L N‚ÇÇ).map N‚ÇÇ.incl =
    posFittingComp R L M := by
  obtain ‚ü®l, hl‚ü© := Filter.eventually_atTop.mp <|
    (eventually_iInf_lowerCentralSeries_eq R L N‚ÇÅ).and <|
    (eventually_iInf_lowerCentralSeries_eq R L N‚ÇÇ).and
    (eventually_iInf_lowerCentralSeries_eq R L M)
  obtain ‚ü®hl‚ÇÅ, hl‚ÇÇ, hl‚ÇÉ‚ü© := hl l (le_refl _)
  simp_rw [‚Üê iInf_lowerCentralSeries_eq_posFittingComp, hl‚ÇÅ, hl‚ÇÇ, hl‚ÇÉ,
    LieSubmodule.lowerCentralSeries_map_eq_lcs, ‚ÜêLieSubmodule.lcs_sup, lowerCentralSeries, h.eq_top]

lemma weightSpace_weightSpaceOf_map_incl (x : L) (œá : L ‚Üí R) :
    (weightSpace (weightSpaceOf M (œá x) x) œá).map (weightSpaceOf M (œá x) x).incl =
    weightSpace M œá := by
  simpa [map_weightSpace_eq_of_injective (weightSpaceOf M (œá x) x).injective_incl]
    using weightSpace_le_weightSpaceOf M x œá

end map_comap

section fitting_decomposition

variable [IsNoetherian R M] [IsArtinian R M]

lemma isCompl_weightSpaceOf_zero_posFittingCompOf (x : L) :
    IsCompl (weightSpaceOf M 0 x) (posFittingCompOf R M x) := by
  simpa only [isCompl_iff, codisjoint_iff, disjoint_iff, ‚Üê LieSubmodule.coe_toSubmodule_eq_iff,
    LieSubmodule.sup_coe_toSubmodule, LieSubmodule.inf_coe_toSubmodule,
    LieSubmodule.top_coeSubmodule, LieSubmodule.bot_coeSubmodule, coe_weightSpaceOf_zero] using
    (toEndomorphism R L M x).isCompl_iSup_ker_pow_iInf_range_pow

/-- This lemma exists only to simplify the proof of
`LieModule.isCompl_weightSpace_zero_posFittingComp`. -/
private lemma isCompl_weightSpace_zero_posFittingComp_aux
    (h : ‚àÄ N < (‚ä§ : LieSubmodule R L M), IsCompl (weightSpace N 0) (posFittingComp R L N)) :
    IsCompl (weightSpace M 0) (posFittingComp R L M) := by
  set M‚ÇÄ := weightSpace M (0 : L ‚Üí R)
  set M‚ÇÅ := posFittingComp R L M
  rcases forall_or_exists_not (fun (x : L) ‚Ü¶ weightSpaceOf M (0 : R) x = ‚ä§)
    with h | ‚ü®x, hx : weightSpaceOf M (0 : R) x ‚â† ‚ä§‚ü©
  ¬∑ suffices IsNilpotent R L M by simp [isCompl_top_bot]
    replace h : M‚ÇÄ = ‚ä§ := by simpa [weightSpace]
    rw [‚Üê LieModule.isNilpotent_of_top_iff', ‚Üê h]
    infer_instance
  ¬∑ set M‚ÇÄ‚Çì := weightSpaceOf M (0 : R) x
    set M‚ÇÅ‚Çì := posFittingCompOf R M x
    set M‚ÇÄ‚Çì‚ÇÄ := weightSpace M‚ÇÄ‚Çì (0 : L ‚Üí R)
    set M‚ÇÄ‚Çì‚ÇÅ := posFittingComp R L M‚ÇÄ‚Çì
    have h‚ÇÅ : IsCompl M‚ÇÄ‚Çì M‚ÇÅ‚Çì := isCompl_weightSpaceOf_zero_posFittingCompOf R L M x
    have h‚ÇÇ : IsCompl M‚ÇÄ‚Çì‚ÇÄ M‚ÇÄ‚Çì‚ÇÅ := h M‚ÇÄ‚Çì hx.lt_top
    have h‚ÇÉ : M‚ÇÄ‚Çì‚ÇÄ.map M‚ÇÄ‚Çì.incl = M‚ÇÄ := by
      rw [map_weightSpace_eq_of_injective M‚ÇÄ‚Çì.injective_incl, inf_eq_left, LieSubmodule.range_incl]
      exact iInf_le _ x
    have h‚ÇÑ : M‚ÇÄ‚Çì‚ÇÅ.map M‚ÇÄ‚Çì.incl ‚äî M‚ÇÅ‚Çì = M‚ÇÅ := by
      apply le_antisymm <| sup_le_iff.mpr
        ‚ü®map_posFittingComp_le _, posFittingCompOf_le_posFittingComp R L M x‚ü©
      rw [‚Üê posFittingComp_map_incl_sup_of_codisjoint h‚ÇÅ.codisjoint]
      exact sup_le_sup_left LieSubmodule.map_incl_le _
    rw [‚Üê h‚ÇÉ, ‚Üê h‚ÇÑ]
    apply Disjoint.isCompl_sup_right_of_isCompl_sup_left
    ¬∑ rw [disjoint_iff, ‚Üê LieSubmodule.map_inf M‚ÇÄ‚Çì.injective_incl, h‚ÇÇ.inf_eq_bot,
        LieSubmodule.map_bot]
    ¬∑ rwa [‚Üê LieSubmodule.map_sup, h‚ÇÇ.sup_eq_top, LieModuleHom.map_top, LieSubmodule.range_incl]

/-- This is the Fitting decomposition of the Lie module `M`. -/
lemma isCompl_weightSpace_zero_posFittingComp :
    IsCompl (weightSpace M 0) (posFittingComp R L M) := by
  let P : LieSubmodule R L M ‚Üí Prop := fun N ‚Ü¶ IsCompl (weightSpace N 0) (posFittingComp R L N)
  suffices : P ‚ä§
  ¬∑ let e := LieModuleEquiv.ofTop R L M
    rw [‚Üê map_weightSpace_eq e, ‚Üê map_posFittingComp_eq e]
    exact (LieSubmodule.orderIsoMapComap e).isCompl_iff.mp this
  refine (LieSubmodule.wellFounded_of_isArtinian R L M).induction (C := P) _ fun N hN ‚Ü¶ ?_
  refine isCompl_weightSpace_zero_posFittingComp_aux R L N fun N' hN' ‚Ü¶ ?_
  suffices : IsCompl (weightSpace (N'.map N.incl) 0) (posFittingComp R L (N'.map N.incl))
  ¬∑ let e := LieSubmodule.equivMapOfInjective N' N.injective_incl
    rw [‚Üê map_weightSpace_eq e, ‚Üê map_posFittingComp_eq e] at this
    exact (LieSubmodule.orderIsoMapComap e).isCompl_iff.mpr this
  exact hN _ (LieSubmodule.map_incl_lt_iff_lt_top.mpr hN')

end fitting_decomposition

lemma disjoint_weightSpaceOf [NoZeroSMulDivisors R M] {x : L} {œÜ‚ÇÅ œÜ‚ÇÇ : R} (h : œÜ‚ÇÅ ‚â† œÜ‚ÇÇ) :
    Disjoint (weightSpaceOf M œÜ‚ÇÅ x) (weightSpaceOf M œÜ‚ÇÇ x) := by
  rw [LieSubmodule.disjoint_iff_coe_toSubmodule]
  exact Module.End.disjoint_iSup_generalizedEigenspace _ h

lemma disjoint_weightSpace [NoZeroSMulDivisors R M] {œá‚ÇÅ œá‚ÇÇ : L ‚Üí R} (h : œá‚ÇÅ ‚â† œá‚ÇÇ) :
    Disjoint (weightSpace M œá‚ÇÅ) (weightSpace M œá‚ÇÇ) := by
  obtain ‚ü®x, hx‚ü© : ‚àÉ x, œá‚ÇÅ x ‚â† œá‚ÇÇ x := Function.ne_iff.mp h
  exact (disjoint_weightSpaceOf R L M hx).mono
    (weightSpace_le_weightSpaceOf M x œá‚ÇÅ) (weightSpace_le_weightSpaceOf M x œá‚ÇÇ)

lemma injOn_weightSpace [NoZeroSMulDivisors R M] :
    InjOn (fun (œá : L ‚Üí R) ‚Ü¶ weightSpace M œá) {œá | weightSpace M œá ‚â† ‚ä•} := by
  rintro œá‚ÇÅ _ œá‚ÇÇ hœá‚ÇÇ (hœá‚ÇÅ‚ÇÇ : weightSpace M œá‚ÇÅ = weightSpace M œá‚ÇÇ)
  contrapose! hœá‚ÇÇ
  simpa [hœá‚ÇÅ‚ÇÇ] using disjoint_weightSpace R L M hœá‚ÇÇ

lemma independent_weightSpace [NoZeroSMulDivisors R M] :
    CompleteLattice.Independent fun (œá : L ‚Üí R) ‚Ü¶ weightSpace M œá := by
  classical
  suffices ‚àÄ œá (s : Finset (L ‚Üí R)) (_ : œá ‚àâ s),
      Disjoint (weightSpace M œá) (s.sup fun (œá : L ‚Üí R) ‚Ü¶ weightSpace M œá) by
    simpa only [CompleteLattice.independent_iff_supIndep_of_injOn (injOn_weightSpace R L M),
      Finset.supIndep_iff_disjoint_erase] using fun s œá _ ‚Ü¶ this _ _ (s.not_mem_erase œá)
  intro œá‚ÇÅ s
  induction' s using Finset.induction_on with œá‚ÇÇ s _ ih; simp
  intro hœá‚ÇÅ‚ÇÇ
  obtain ‚ü®hœá‚ÇÅ‚ÇÇ : œá‚ÇÅ ‚â† œá‚ÇÇ, hœá‚ÇÅ : œá‚ÇÅ ‚àâ s‚ü© := by rwa [Finset.mem_insert, not_or] at hœá‚ÇÅ‚ÇÇ
  specialize ih hœá‚ÇÅ
  rw [Finset.sup_insert, disjoint_iff, LieSubmodule.eq_bot_iff]
  rintro x ‚ü®hx, hx'‚ü©
  simp only [SetLike.mem_coe, LieSubmodule.mem_coeSubmodule] at hx hx'
  suffices x ‚àà weightSpace M œá‚ÇÇ by
    rw [‚Üê LieSubmodule.mem_bot (R := R) (L := L), ‚Üê (disjoint_weightSpace R L M hœá‚ÇÅ‚ÇÇ).eq_bot]
    exact ‚ü®hx, this‚ü©
  obtain ‚ü®y, hy, z, hz, rfl‚ü© := (LieSubmodule.mem_sup _ _ _).mp hx'; clear hx'
  suffices ‚àÄ l, ‚àÉ (k : ‚Ñï),
      ((toEndomorphism R L M l - algebraMap R (Module.End R M) (œá‚ÇÇ l)) ^ k) (y + z) ‚àà
      weightSpace M œá‚ÇÅ ‚äì Finset.sup s fun œá ‚Ü¶ weightSpace M œá by
    simpa only [ih.eq_bot, LieSubmodule.mem_bot, mem_weightSpace] using this
  intro l
  let g : Module.End R M := toEndomorphism R L M l - algebraMap R (Module.End R M) (œá‚ÇÇ l)
  obtain ‚ü®k, hk : (g ^ k) y = 0‚ü© := (mem_weightSpace _ _ _).mp hy l
  refine ‚ü®k, (LieSubmodule.mem_inf _ _ _).mp ‚ü®?_, ?_‚ü©‚ü©
  ¬∑ exact LieSubmodule.mapsTo_pow_toEndomorphism_sub_algebraMap _ hx
  ¬∑ rw [map_add, hk, zero_add]
    suffices (s.sup fun œá ‚Ü¶ weightSpace M œá : Submodule R M).map (g ^ k) ‚â§
        s.sup fun œá ‚Ü¶ weightSpace M œá by
      refine this (Submodule.mem_map_of_mem ?_)
      simp_rw [‚Üê LieSubmodule.mem_coeSubmodule, Finset.sup_eq_iSup,
        LieSubmodule.iSup_coe_toSubmodule, ‚Üê Finset.sup_eq_iSup] at hz
      exact hz
    simp_rw [Finset.sup_eq_iSup, Submodule.map_iSup (Œπ := L ‚Üí R), Submodule.map_iSup (Œπ := _ ‚àà s),
      LieSubmodule.iSup_coe_toSubmodule]
    refine iSup‚ÇÇ_mono fun œá _ ‚Ü¶ ?_
    rintro - ‚ü®u, hu, rfl‚ü©
    exact LieSubmodule.mapsTo_pow_toEndomorphism_sub_algebraMap _ hu

lemma independent_weightSpaceOf [NoZeroSMulDivisors R M] (x : L) :
    CompleteLattice.Independent fun (œá : R) ‚Ü¶ weightSpaceOf M œá x := by
  rw [LieSubmodule.independent_iff_coe_toSubmodule]
  exact (toEndomorphism R L M x).independent_generalizedEigenspace

lemma finite_weightSpaceOf_ne_bot [NoZeroSMulDivisors R M] [IsNoetherian R M] (x : L) :
    {œá : R | weightSpaceOf M œá x ‚â† ‚ä•}.Finite :=
  CompleteLattice.WellFounded.finite_ne_bot_of_independent
    (LieSubmodule.wellFounded_of_noetherian R L M) (independent_weightSpaceOf R L M x)

lemma finite_weightSpace_ne_bot [NoZeroSMulDivisors R M] [IsNoetherian R M] :
    {œá : L ‚Üí R | weightSpace M œá ‚â† ‚ä•}.Finite :=
  CompleteLattice.WellFounded.finite_ne_bot_of_independent
    (LieSubmodule.wellFounded_of_noetherian R L M) (independent_weightSpace R L M)

/-- A Lie module `M` of a Lie algebra `L` is triangularizable if the endomorhpism of `M` defined by
any `x : L` is triangularizable. -/
class IsTriangularizable : Prop :=
  iSup_eq_top : ‚àÄ x, ‚®Ü œÜ, ‚®Ü k, (toEndomorphism R L M x).generalizedEigenspace œÜ k = ‚ä§

@[simp]
lemma iSup_weightSpaceOf_eq_top [IsTriangularizable R L M] (x : L) :
    ‚®Ü (œÜ : R), weightSpaceOf M œÜ x = ‚ä§ := by
  rw [‚Üê LieSubmodule.coe_toSubmodule_eq_iff, LieSubmodule.iSup_coe_toSubmodule,
    LieSubmodule.top_coeSubmodule]
  exact IsTriangularizable.iSup_eq_top x

section field

open FiniteDimensional

variable (K)
variable [Field K] [LieAlgebra K L] [Module K M] [LieModule K L M] [LieAlgebra.IsNilpotent K L]
  [FiniteDimensional K M]

instance instIsTriangularizableOfIsAlgClosed [IsAlgClosed K] : IsTriangularizable K L M :=
  ‚ü®fun _ ‚Ü¶ Module.End.iSup_generalizedEigenspace_eq_top _‚ü©

instance (N : LieSubmodule K L M) [IsTriangularizable K L M] : IsTriangularizable K L N := by
  refine ‚ü®fun y ‚Ü¶ ?_‚ü©
  rw [‚Üê N.toEndomorphism_restrict_eq_toEndomorphism y]
  exact Module.End.iSup_generalizedEigenspace_restrict_eq_top _ (IsTriangularizable.iSup_eq_top y)

lemma iSup_weightSpace_eq_top [IsTriangularizable K L M] :
    ‚®Ü œá : L ‚Üí K, weightSpace M œá = ‚ä§ := by
  clear! R -- cf https://github.com/leanprover/lean4/issues/2452
  induction' h_dim : finrank K M using Nat.strong_induction_on with n ih generalizing M
  obtain h' | ‚ü®y : L, hy : ¬¨ ‚àÉ œÜ, weightSpaceOf M œÜ y = ‚ä§‚ü© :=
    forall_or_exists_not (fun (x : L) ‚Ü¶ ‚àÉ (œÜ : K), weightSpaceOf M œÜ x = ‚ä§)
  ¬∑ choose œá hœá using h'
    replace hœá : weightSpace M œá = ‚ä§ := by simpa only [weightSpace, hœá] using iInf_top
    exact eq_top_iff.mpr <| hœá ‚ñ∏ le_iSup (weightSpace M) œá
  ¬∑ replace hy : ‚àÄ œÜ, finrank K (weightSpaceOf M œÜ y) < n := fun œÜ ‚Ü¶ by
      simp_rw [not_exists, ‚Üê lt_top_iff_ne_top] at hy; exact h_dim ‚ñ∏ Submodule.finrank_lt (hy œÜ)
    replace ih : ‚àÄ œÜ, ‚®Ü œá : L ‚Üí K, weightSpace (weightSpaceOf M œÜ y) œá = ‚ä§ :=
      fun œÜ ‚Ü¶ ih _ (hy œÜ) (weightSpaceOf M œÜ y) rfl
    replace ih : ‚àÄ œÜ, ‚®Ü (œá : L ‚Üí K) (_ : œá y = œÜ), weightSpace (weightSpaceOf M œÜ y) œá = ‚ä§ := by
      intro œÜ
      suffices ‚àÄ œá : L ‚Üí K, œá y ‚â† œÜ ‚Üí weightSpace (weightSpaceOf M œÜ y) œá = ‚ä• by
        specialize ih œÜ; rw [iSup_split, biSup_congr this] at ih; simpa using ih
      intro œá hœá
      rw [eq_bot_iff, ‚Üê (weightSpaceOf M œÜ y).ker_incl, LieModuleHom.ker,
        ‚Üê LieSubmodule.map_le_iff_le_comap, map_weightSpace_eq_of_injective
        (weightSpaceOf M œÜ y).injective_incl, LieSubmodule.range_incl, ‚Üê disjoint_iff_inf_le]
      exact (disjoint_weightSpaceOf K L M hœá).mono_left (weightSpace_le_weightSpaceOf M y œá)
    replace ih : ‚àÄ œÜ, ‚®Ü (œá : L ‚Üí K) (_ : œá y = œÜ), weightSpace M œá = weightSpaceOf M œÜ y := by
      intro œÜ
      have : ‚àÄ (œá : L ‚Üí K) (_ : œá y = œÜ), weightSpace M œá =
          (weightSpace (weightSpaceOf M œÜ y) œá).map (weightSpaceOf M œÜ y).incl := fun œá hœá ‚Ü¶ by
        rw [‚Üê hœá, weightSpace_weightSpaceOf_map_incl]
      simp_rw [biSup_congr this, ‚Üê LieSubmodule.map_iSup, ih, LieModuleHom.map_top,
        LieSubmodule.range_incl]
    simpa only [‚Üêih, iSup_comm (Œπ := K), iSup_iSup_eq_right] using iSup_weightSpaceOf_eq_top K L M y

end field

end LieModule
