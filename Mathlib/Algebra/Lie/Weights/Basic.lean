/-
Copyright (c) 2021 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.Algebra.Ring.Divisibility.Lemmas
import Mathlib.Algebra.Lie.Nilpotent
import Mathlib.Algebra.Lie.TensorProduct
import Mathlib.Algebra.Lie.Engel
import Mathlib.LinearAlgebra.Eigenspace.Basic
import Mathlib.LinearAlgebra.TensorProduct.Tower
import Mathlib.RingTheory.Artinian

#align_import algebra.lie.weights from "leanprover-community/mathlib"@"6b0169218d01f2837d79ea2784882009a0da1aa1"

/-!
# Weight spaces of Lie modules of nilpotent Lie algebras

Just as a key tool when studying the behaviour of a linear operator is to decompose the space on
which it acts into a sum of (generalised) eigenspaces, a key tool when studying a representation `M`
of Lie algebra `L` is to decompose `M` into a sum of simultaneous eigenspaces of `x` as `x` ranges
over `L`. These simultaneous generalised eigenspaces are known as the weight spaces of `M`.

When `L` is nilpotent, it follows from the binomial theorem that weight spaces are Lie submodules.

Basic definitions and properties of the above ideas are provided in this file.

## Main definitions

  * `LieModule.weightSpaceOf`
  * `LieModule.weightSpace`
  * `LieModule.posFittingCompOf`
  * `LieModule.posFittingComp`
  * `LieModule.iSup_ucs_eq_weightSpace_zero`
  * `LieModule.iInf_lowerCentralSeries_eq_posFittingComp`

## References

* [N. Bourbaki, *Lie Groups and Lie Algebras, Chapters 7--9*](bourbaki1975b)

## Tags

lie character, eigenvalue, eigenspace, weight, weight vector, root, root vector
-/

variable {R L M : Type*} [CommRing R] [LieRing L] [LieAlgebra R L] [LieAlgebra.IsNilpotent R L]
  [AddCommGroup M] [Module R M] [LieRingModule L M] [LieModule R L M]

namespace LieModule

open LieAlgebra TensorProduct TensorProduct.LieModule
open scoped BigOperators TensorProduct

section notation_weight_space_of

/-- Until we define `LieModule.weightSpaceOf`, it is useful to have some notation as follows: -/
local notation3 (prettyPrint := false) "ùïé("M"," œá"," x")" =>
  (toEndomorphism R L M x).maximalGeneralizedEigenspace œá

/-- See also `bourbaki1975b` Chapter VII ¬ß1.1, Proposition 2 (ii). -/
protected theorem weight_vector_multiplication (M‚ÇÅ M‚ÇÇ M‚ÇÉ : Type*)
    [AddCommGroup M‚ÇÅ] [Module R M‚ÇÅ] [LieRingModule L M‚ÇÅ] [LieModule R L M‚ÇÅ] [AddCommGroup M‚ÇÇ]
    [Module R M‚ÇÇ] [LieRingModule L M‚ÇÇ] [LieModule R L M‚ÇÇ] [AddCommGroup M‚ÇÉ] [Module R M‚ÇÉ]
    [LieRingModule L M‚ÇÉ] [LieModule R L M‚ÇÉ] (g : M‚ÇÅ ‚äó[R] M‚ÇÇ ‚Üí‚Çó‚ÅÖR,L‚ÅÜ M‚ÇÉ) (œá‚ÇÅ œá‚ÇÇ : R) (x : L) :
    LinearMap.range ((g : M‚ÇÅ ‚äó[R] M‚ÇÇ ‚Üí‚Çó[R] M‚ÇÉ).comp (mapIncl ùïé(M‚ÇÅ, œá‚ÇÅ, x) ùïé(M‚ÇÇ, œá‚ÇÇ, x))) ‚â§
      ùïé(M‚ÇÉ, œá‚ÇÅ + œá‚ÇÇ, x) := by
  -- Unpack the statement of the goal.
  intro m‚ÇÉ
  simp only [TensorProduct.mapIncl, LinearMap.mem_range, LinearMap.coe_comp,
    LieModuleHom.coe_toLinearMap, Function.comp_apply, Pi.add_apply, exists_imp,
    Module.End.mem_maximalGeneralizedEigenspace]
  rintro t rfl
  -- Set up some notation.
  let F : Module.End R M‚ÇÉ := toEndomorphism R L M‚ÇÉ x - (œá‚ÇÅ + œá‚ÇÇ) ‚Ä¢ ‚Üë1
  -- The goal is linear in `t` so use induction to reduce to the case that `t` is a pure tensor.
  refine t.induction_on ?_ ?_ ?_
  ¬∑ use 0; simp only [LinearMap.map_zero, LieModuleHom.map_zero]
  swap
  ¬∑ rintro t‚ÇÅ t‚ÇÇ ‚ü®k‚ÇÅ, hk‚ÇÅ‚ü© ‚ü®k‚ÇÇ, hk‚ÇÇ‚ü©; use max k‚ÇÅ k‚ÇÇ
    simp only [LieModuleHom.map_add, LinearMap.map_add,
      LinearMap.pow_map_zero_of_le (le_max_left k‚ÇÅ k‚ÇÇ) hk‚ÇÅ,
      LinearMap.pow_map_zero_of_le (le_max_right k‚ÇÅ k‚ÇÇ) hk‚ÇÇ, add_zero]
  -- Now the main argument: pure tensors.
  rintro ‚ü®m‚ÇÅ, hm‚ÇÅ‚ü© ‚ü®m‚ÇÇ, hm‚ÇÇ‚ü©
  change ‚àÉ k, (F ^ k) ((g : M‚ÇÅ ‚äó[R] M‚ÇÇ ‚Üí‚Çó[R] M‚ÇÉ) (m‚ÇÅ ‚äó‚Çú m‚ÇÇ)) = (0 : M‚ÇÉ)
  -- Eliminate `g` from the picture.
  let f‚ÇÅ : Module.End R (M‚ÇÅ ‚äó[R] M‚ÇÇ) := (toEndomorphism R L M‚ÇÅ x - œá‚ÇÅ ‚Ä¢ ‚Üë1).rTensor M‚ÇÇ
  let f‚ÇÇ : Module.End R (M‚ÇÅ ‚äó[R] M‚ÇÇ) := (toEndomorphism R L M‚ÇÇ x - œá‚ÇÇ ‚Ä¢ ‚Üë1).lTensor M‚ÇÅ
  have h_comm_square : F ‚àò‚Çó ‚Üëg = (g : M‚ÇÅ ‚äó[R] M‚ÇÇ ‚Üí‚Çó[R] M‚ÇÉ).comp (f‚ÇÅ + f‚ÇÇ) := by
    ext m‚ÇÅ m‚ÇÇ;
    simp only [‚Üê g.map_lie x (m‚ÇÅ ‚äó‚Çú m‚ÇÇ), add_smul, sub_tmul, tmul_sub, smul_tmul, lie_tmul_right,
      tmul_smul, toEndomorphism_apply_apply, LieModuleHom.map_smul, LinearMap.one_apply,
      LieModuleHom.coe_toLinearMap, LinearMap.smul_apply, Function.comp_apply, LinearMap.coe_comp,
      LinearMap.rTensor_tmul, LieModuleHom.map_add, LinearMap.add_apply, LieModuleHom.map_sub,
      LinearMap.sub_apply, LinearMap.lTensor_tmul, AlgebraTensorModule.curry_apply,
      curry_apply, LinearMap.toFun_eq_coe, LinearMap.coe_restrictScalars]
    abel
  rsuffices ‚ü®k, hk‚ü© : ‚àÉ k : ‚Ñï, ((f‚ÇÅ + f‚ÇÇ) ^ k) (m‚ÇÅ ‚äó‚Çú m‚ÇÇ) = 0
  ¬∑ use k
    change (F ^ k) (g.toLinearMap (m‚ÇÅ ‚äó‚Çú[R] m‚ÇÇ)) = 0
    rw [‚Üê LinearMap.comp_apply, LinearMap.commute_pow_left_of_commute h_comm_square,
      LinearMap.comp_apply, hk, LinearMap.map_zero]
  -- Unpack the information we have about `m‚ÇÅ`, `m‚ÇÇ`.
  simp only [Module.End.mem_maximalGeneralizedEigenspace] at hm‚ÇÅ hm‚ÇÇ
  obtain ‚ü®k‚ÇÅ, hk‚ÇÅ‚ü© := hm‚ÇÅ
  obtain ‚ü®k‚ÇÇ, hk‚ÇÇ‚ü© := hm‚ÇÇ
  have hf‚ÇÅ : (f‚ÇÅ ^ k‚ÇÅ) (m‚ÇÅ ‚äó‚Çú m‚ÇÇ) = 0 := by
    simp only [hk‚ÇÅ, zero_tmul, LinearMap.rTensor_tmul, LinearMap.rTensor_pow]
  have hf‚ÇÇ : (f‚ÇÇ ^ k‚ÇÇ) (m‚ÇÅ ‚äó‚Çú m‚ÇÇ) = 0 := by
    simp only [hk‚ÇÇ, tmul_zero, LinearMap.lTensor_tmul, LinearMap.lTensor_pow]
  -- It's now just an application of the binomial theorem.
  use k‚ÇÅ + k‚ÇÇ - 1
  have hf_comm : Commute f‚ÇÅ f‚ÇÇ := by
    ext m‚ÇÅ m‚ÇÇ
    simp only [LinearMap.mul_apply, LinearMap.rTensor_tmul, LinearMap.lTensor_tmul,
      AlgebraTensorModule.curry_apply, LinearMap.toFun_eq_coe, LinearMap.lTensor_tmul,
      curry_apply, LinearMap.coe_restrictScalars]
  rw [hf_comm.add_pow']
  simp only [TensorProduct.mapIncl, Submodule.subtype_apply, Finset.sum_apply, Submodule.coe_mk,
    LinearMap.coeFn_sum, TensorProduct.map_tmul, LinearMap.smul_apply]
  -- The required sum is zero because each individual term is zero.
  apply Finset.sum_eq_zero
  rintro ‚ü®i, j‚ü© hij
  -- Eliminate the binomial coefficients from the picture.
  suffices (f‚ÇÅ ^ i * f‚ÇÇ ^ j) (m‚ÇÅ ‚äó‚Çú m‚ÇÇ) = 0 by rw [this]; apply smul_zero
  -- Finish off with appropriate case analysis.
  cases' Nat.le_or_le_of_add_eq_add_pred (Finset.Nat.mem_antidiagonal.mp hij) with hi hj
  ¬∑ rw [(hf_comm.pow_pow i j).eq, LinearMap.mul_apply, LinearMap.pow_map_zero_of_le hi hf‚ÇÅ,
      LinearMap.map_zero]
  ¬∑ rw [LinearMap.mul_apply, LinearMap.pow_map_zero_of_le hj hf‚ÇÇ, LinearMap.map_zero]

lemma lie_mem_maxGenEigenspace_toEndomorphism
    {œá‚ÇÅ œá‚ÇÇ : R} {x y : L} {m : M} (hy : y ‚àà ùïé(L, œá‚ÇÅ, x)) (hm : m ‚àà ùïé(M, œá‚ÇÇ, x)) :
    ‚ÅÖy, m‚ÅÜ ‚àà ùïé(M, œá‚ÇÅ + œá‚ÇÇ, x) := by
  apply LieModule.weight_vector_multiplication L M M (toModuleHom R L M) œá‚ÇÅ œá‚ÇÇ
  simp only [LieModuleHom.coe_toLinearMap, Function.comp_apply, LinearMap.coe_comp,
    TensorProduct.mapIncl, LinearMap.mem_range]
  use ‚ü®y, hy‚ü© ‚äó‚Çú ‚ü®m, hm‚ü©
  simp only [Submodule.subtype_apply, toModuleHom_apply, TensorProduct.map_tmul]

variable (M)

/-- If `M` is a representation of a nilpotent Lie algebra `L`, `œá` is a scalar, and `x : L`, then
`weightSpaceOf M œá x` is the maximal generalized `œá`-eigenspace of the action of `x` on `M`.

It is a Lie submodule because `L` is nilpotent. -/
def weightSpaceOf (œá : R) (x : L) : LieSubmodule R L M :=
  { ùïé(M, œá, x) with
    lie_mem := by
      intro y m hm
      simp only [AddSubsemigroup.mem_carrier, AddSubmonoid.mem_toSubsemigroup,
        Submodule.mem_toAddSubmonoid] at hm ‚ä¢
      rw [‚Üê zero_add œá]
      exact lie_mem_maxGenEigenspace_toEndomorphism (by simp) hm }

theorem mem_weightSpaceOf (œá : R) (x : L) (m : M) :
    m ‚àà weightSpaceOf M œá x ‚Üî ‚àÉ k : ‚Ñï, ((toEndomorphism R L M x - œá ‚Ä¢ ‚Üë1) ^ k) m = 0 := by
  simp [weightSpaceOf]

end notation_weight_space_of

variable (M)

/-- If `M` is a representation of a nilpotent Lie algebra `L` and `œá : L ‚Üí R` is a family of
scalars, then `weightSpace M œá` is the intersection of the maximal generalized `œá x`-eigenspaces of
the action of `x` on `M` as `x` ranges over `L`.

It is a Lie submodule because `L` is nilpotent. -/
def weightSpace (œá : L ‚Üí R) : LieSubmodule R L M :=
  ‚®Ö x, weightSpaceOf M (œá x) x

theorem mem_weightSpace (œá : L ‚Üí R) (m : M) :
    m ‚àà weightSpace M œá ‚Üî ‚àÄ x, ‚àÉ k : ‚Ñï, ((toEndomorphism R L M x - œá x ‚Ä¢ ‚Üë1) ^ k) m = 0 := by
  simp [weightSpace, mem_weightSpaceOf]

/-- See also the more useful form `LieModule.zero_weightSpace_eq_top_of_nilpotent`. -/
@[simp]
theorem zero_weightSpace_eq_top_of_nilpotent' [IsNilpotent R L M] :
    weightSpace M (0 : L ‚Üí R) = ‚ä§ := by
  ext
  simp [weightSpace, weightSpaceOf]
#align lie_module.zero_weight_space_eq_top_of_nilpotent' LieModule.zero_weightSpace_eq_top_of_nilpotent'

theorem coe_weightSpace_of_top (œá : L ‚Üí R) :
    (weightSpace M (œá ‚àò (‚ä§ : LieSubalgebra R L).incl) : Submodule R M) = weightSpace M œá := by
  ext m
  simp only [mem_weightSpace, LieSubmodule.mem_coeSubmodule, Subtype.forall]
  apply forall_congr'
  simp
#align lie_module.coe_weight_space_of_top LieModule.coe_weightSpace_of_top

@[simp]
theorem zero_weightSpace_eq_top_of_nilpotent [IsNilpotent R L M] :
    weightSpace M (0 : (‚ä§ : LieSubalgebra R L) ‚Üí R) = ‚ä§ := by
  ext m
  simp only [mem_weightSpace, Pi.zero_apply, zero_smul, sub_zero, Subtype.forall, forall_true_left,
    LieSubalgebra.toEndomorphism_mk, LieSubalgebra.mem_top, LieSubmodule.mem_top, iff_true]
  intro x
  obtain ‚ü®k, hk‚ü© := exists_forall_pow_toEndomorphism_eq_zero R L M
  exact ‚ü®k, by simp [hk x]‚ü©
#align lie_module.zero_weight_space_eq_top_of_nilpotent LieModule.zero_weightSpace_eq_top_of_nilpotent

variable (R) in
theorem exists_weightSpace_zero_le_ker_of_isNoetherian
    [IsNoetherian R M] (x : L) :
    ‚àÉ k : ‚Ñï, weightSpace M (0 : L ‚Üí R) ‚â§ LinearMap.ker (toEndomorphism R L M x ^ k) := by
  use (toEndomorphism R L M x).maximalGeneralizedEigenspaceIndex 0
  simp only [weightSpace, weightSpaceOf, LieSubmodule.iInf_coe_toSubmodule, Pi.zero_apply, iInf_le,
    ‚Üê Module.End.generalizedEigenspace_zero,
    ‚Üê (toEndomorphism R L M x).maximalGeneralizedEigenspace_eq]

/-- A (nilpotent) Lie algebra acts nilpotently on the zero weight space of a Noetherian Lie
module. -/
theorem isNilpotent_toEndomorphism_weightSpace_zero [IsNoetherian R M]
    (x : L) : _root_.IsNilpotent <| toEndomorphism R L (weightSpace M (0 : L ‚Üí R)) x := by
  obtain ‚ü®k, hk‚ü© := exists_weightSpace_zero_le_ker_of_isNoetherian R M x
  use k
  ext ‚ü®m, hm‚ü©
  rw [LinearMap.zero_apply, LieSubmodule.coe_zero, Submodule.coe_eq_zero, ‚Üê
    LieSubmodule.toEndomorphism_restrict_eq_toEndomorphism, LinearMap.pow_restrict, ‚Üê
    SetLike.coe_eq_coe, LinearMap.restrict_apply, Submodule.coe_mk, Submodule.coe_zero]
  exact hk hm
#align lie_module.is_nilpotent_to_endomorphism_weight_space_zero LieModule.isNilpotent_toEndomorphism_weightSpace_zero

/-- By Engel's theorem, the zero weight space of a Noetherian Lie module is nilpotent. -/
instance [IsNoetherian R M] :
    IsNilpotent R L (weightSpace M (0 : L ‚Üí R)) :=
  isNilpotent_iff_forall'.mpr <| isNilpotent_toEndomorphism_weightSpace_zero M

variable (R L)

@[simp]
lemma weightSpace_zero_normalizer_eq_self :
    (weightSpace M (0 : L ‚Üí R)).normalizer = weightSpace M 0 := by
  refine' le_antisymm _ (LieSubmodule.le_normalizer _)
  intro m hm
  rw [LieSubmodule.mem_normalizer] at hm
  simp only [mem_weightSpace, Pi.zero_apply, zero_smul, sub_zero] at hm ‚ä¢
  intro y
  obtain ‚ü®k, hk‚ü© := hm y y
  use k + 1
  simpa [pow_succ', LinearMap.mul_eq_comp]

lemma iSup_ucs_le_weightSpace_zero :
    ‚®Ü k, (‚ä• : LieSubmodule R L M).ucs k ‚â§ weightSpace M (0 : L ‚Üí R) := by
  simpa using LieSubmodule.ucs_le_of_normalizer_eq_self (weightSpace_zero_normalizer_eq_self R L M)

/-- See also `LieModule.iInf_lowerCentralSeries_eq_posFittingComp`. -/
lemma iSup_ucs_eq_weightSpace_zero [IsNoetherian R M] :
    ‚®Ü k, (‚ä• : LieSubmodule R L M).ucs k = weightSpace M (0 : L ‚Üí R) := by
  obtain ‚ü®k, hk‚ü© := (LieSubmodule.isNilpotent_iff_exists_self_le_ucs
    <| weightSpace M (0 : L ‚Üí R)).mp inferInstance
  refine le_antisymm (iSup_ucs_le_weightSpace_zero R L M) (le_trans hk ?_)
  exact le_iSup (fun k ‚Ü¶ (‚ä• : LieSubmodule R L M).ucs k) k

variable {L}

/-- If `M` is a representation of a nilpotent Lie algebra `L`, and `x : L`, then
`posFittingCompOf R M x` is the infimum of the decreasing system
`range œÜ‚Çì ‚äá range œÜ‚Çì¬≤ ‚äá range œÜ‚Çì¬≥ ‚äá ‚ãØ` where `œÜ‚Çì : End R M := toEndomorphism R L M x`. We call this
the "positive Fitting component" because with appropriate assumptions (e.g., `R` is a field and
`M` is finite-dimensional) `œÜ‚Çì` induces the so-called Fitting decomposition: `M = M‚ÇÄ ‚äï M‚ÇÅ` where
`M‚ÇÄ = weightSpaceOf M 0 x` and `M‚ÇÅ = posFittingCompOf R M x`.

It is a Lie submodule because `L` is nilpotent. -/
def posFittingCompOf (x : L) : LieSubmodule R L M :=
  { toSubmodule := ‚®Ö k, LinearMap.range (toEndomorphism R L M x ^ k)
    lie_mem := by
      set œÜ := toEndomorphism R L M x
      intros y m hm
      simp only [AddSubsemigroup.mem_carrier, AddSubmonoid.mem_toSubsemigroup,
        Submodule.mem_toAddSubmonoid, Submodule.mem_iInf, LinearMap.mem_range] at hm ‚ä¢
      intro k
      obtain ‚ü®N, hN‚ü© := LieAlgebra.nilpotent_ad_of_nilpotent_algebra R L
      obtain ‚ü®m, rfl‚ü© := hm (N + k)
      let f‚ÇÅ : Module.End R (L ‚äó[R] M) := (LieAlgebra.ad R L x).rTensor M
      let f‚ÇÇ : Module.End R (L ‚äó[R] M) := œÜ.lTensor L
      replace hN : f‚ÇÅ ^ N = 0 := by ext; simp [hN]
      have h‚ÇÅ : Commute f‚ÇÅ f‚ÇÇ := by ext; simp
      have h‚ÇÇ : œÜ ‚àò‚Çó toModuleHom R L M = toModuleHom R L M ‚àò‚Çó (f‚ÇÅ + f‚ÇÇ) := by ext; simp
      obtain ‚ü®q, hq‚ü© := h‚ÇÅ.add_pow_dvd_pow_of_pow_eq_zero_right (N + k).le_succ hN
      use toModuleHom R L M (q (y ‚äó‚Çú m))
      change (œÜ ^ k).comp ((toModuleHom R L M : L ‚äó[R] M ‚Üí‚Çó[R] M)) _ = _
      simp [LinearMap.commute_pow_left_of_commute h‚ÇÇ, LinearMap.comp_apply (g := (f‚ÇÅ + f‚ÇÇ) ^ k),
        ‚Üê LinearMap.comp_apply (g := q), ‚Üê LinearMap.mul_eq_comp, ‚Üê hq] }

variable {M} in
lemma mem_posFittingCompOf (x : L) (m : M) :
    m ‚àà posFittingCompOf R M x ‚Üî ‚àÄ (k : ‚Ñï), ‚àÉ n, (toEndomorphism R L M x ^ k) n = m := by
  simp [posFittingCompOf]

@[simp] lemma posFittingCompOf_le_lowerCentralSeries (x : L) (k : ‚Ñï) :
    posFittingCompOf R M x ‚â§ lowerCentralSeries R L M k := by
  suffices : ‚àÄ m l, (toEndomorphism R L M x ^ l) m ‚àà lowerCentralSeries R L M l
  ¬∑ intro m hm
    obtain ‚ü®n, rfl‚ü© := (mem_posFittingCompOf R x m).mp hm k
    exact this n k
  intro m l
  induction' l with l ih; simp
  simp only [lowerCentralSeries_succ, pow_succ, LinearMap.mul_apply]
  exact LieSubmodule.lie_mem_lie _ ‚ä§ (LieSubmodule.mem_top x) ih

@[simp] lemma posFittingCompOf_eq_bot_of_isNilpotent
    [IsNilpotent R L M] (x : L) :
    posFittingCompOf R M x = ‚ä• := by
  simp_rw [eq_bot_iff, ‚Üê iInf_lowerCentralSeries_eq_bot_of_isNilpotent, le_iInf_iff,
    posFittingCompOf_le_lowerCentralSeries, forall_const]

variable (L)

/-- If `M` is a representation of a nilpotent Lie algebra `L` with coefficients in `R`, then
`posFittingComp R L M` is the span of the positive Fitting components of the action of `x` on `M`,
as `x` ranges over `L`.

It is a Lie submodule because `L` is nilpotent. -/
def posFittingComp : LieSubmodule R L M :=
  ‚®Ü x, posFittingCompOf R M x

lemma mem_posFittingComp (m : M) :
    m ‚àà posFittingComp R L M ‚Üî m ‚àà ‚®Ü (x : L), posFittingCompOf R M x := by
  rfl

lemma posFittingCompOf_le_posFittingComp (x : L) :
    posFittingCompOf R M x ‚â§ posFittingComp R L M := by
  rw [posFittingComp]; exact le_iSup (posFittingCompOf R M) x

lemma posFittingComp_le_iInf_lowerCentralSeries :
    posFittingComp R L M ‚â§ ‚®Ö k, lowerCentralSeries R L M k := by
  simp [posFittingComp]

/-- See also `LieModule.iSup_ucs_eq_weightSpace_zero`. -/
@[simp] lemma iInf_lowerCentralSeries_eq_posFittingComp
    [IsNoetherian R M] [IsArtinian R M] :
    ‚®Ö k, lowerCentralSeries R L M k = posFittingComp R L M := by
  refine le_antisymm ?_ (posFittingComp_le_iInf_lowerCentralSeries R L M)
  apply iInf_lcs_le_of_isNilpotent_quot
  rw [LieModule.isNilpotent_iff_forall']
  intro x
  obtain ‚ü®k, hk‚ü© := Filter.eventually_atTop.mp (toEndomorphism R L M x).eventually_iInf_range_pow_eq
  use k
  ext ‚ü®m‚ü©
  set F := posFittingComp R L M
  replace hk : (toEndomorphism R L M x ^ k) m ‚àà F := by
    apply posFittingCompOf_le_posFittingComp R L M x
    simp_rw [‚Üê LieSubmodule.mem_coeSubmodule, posFittingCompOf, hk k (le_refl k)]
    apply LinearMap.mem_range_self
  suffices (toEndomorphism R L (M ‚ß∏ F) x ^ k) (LieSubmodule.Quotient.mk (N := F) m) =
    LieSubmodule.Quotient.mk (N := F) ((toEndomorphism R L M x ^ k) m) by simpa [this]
  have := LinearMap.congr_fun (LinearMap.commute_pow_left_of_commute
    (LieSubmodule.Quotient.toEndomorphism_comp_mk' F x) k) m
  simpa using this

@[simp] lemma posFittingComp_eq_bot_of_isNilpotent
    [IsNilpotent R L M] :
    posFittingComp R L M = ‚ä• := by
  simp [posFittingComp]

end LieModule
