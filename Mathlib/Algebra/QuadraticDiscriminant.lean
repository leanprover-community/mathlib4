/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou
-/
import Mathlib.Algebra.CharP.Invertible
import Mathlib.Order.Filter.AtTopBot
import Mathlib.Tactic.FieldSimp
import Mathlib.Tactic.LinearCombination

#align_import algebra.quadratic_discriminant from "leanprover-community/mathlib"@"e085d1df33274f4b32f611f483aae678ba0b42df"

/-!
# Quadratic discriminants and roots of a quadratic

This file defines the discriminant of a quadratic and gives the solution to a quadratic equation.

## Main definition

- `discrim a b c`: the discriminant of a quadratic `a * x * x + b * x + c` is `b * b - 4 * a * c`.

## Main statements

- `quadratic_eq_zero_iff`: roots of a quadratic can be written as
  `(-b + s) / (2 * a)` or `(-b - s) / (2 * a)`, where `s` is a square root of the discriminant.
- `quadratic_ne_zero_of_discrim_ne_sq`: if the discriminant has no square root,
  then the corresponding quadratic has no root.
- `discrim_le_zero`: if a quadratic is always non-negative, then its discriminant is non-positive.
- `discrim_le_zero_of_nonpos`, `discrim_lt_zero`, `discrim_lt_zero_of_neg`: versions of this
  statement with other inequalities.

## Tags

polynomial, quadratic, discriminant, root
-/


open Filter

section Ring

variable {R : Type*}

/-- Discriminant of a quadratic -/
def discrim [Ring R] (a b c : R) : R :=
  b ^ 2 - 4 * a * c
#align discrim discrim

@[simp] lemma discrim_neg [Ring R] (a b c : R) : discrim (-a) (-b) (-c) = discrim a b c := by
  simp [discrim]
  -- ðŸŽ‰ no goals
#align discrim_neg discrim_neg

variable [CommRing R] {a b c : R}

lemma discrim_eq_sq_of_quadratic_eq_zero {x : R} (h : a * x * x + b * x + c = 0) :
    discrim a b c = (2 * a * x + b) ^ 2 := by
  rw [discrim]
  -- âŠ¢ b ^ 2 - 4 * a * c = (2 * a * x + b) ^ 2
  linear_combination -4 * a * h
  -- ðŸŽ‰ no goals
#align discrim_eq_sq_of_quadratic_eq_zero discrim_eq_sq_of_quadratic_eq_zero

/-- A quadratic has roots if and only if its discriminant equals some square.
-/
theorem quadratic_eq_zero_iff_discrim_eq_sq [NeZero (2 : R)] [NoZeroDivisors R]
    (ha : a â‰  0) (x : R) :
    a * x * x + b * x + c = 0 â†” discrim a b c = (2 * a * x + b) ^ 2 := by
  refine âŸ¨discrim_eq_sq_of_quadratic_eq_zero, fun h â†¦ ?_âŸ©
  -- âŠ¢ a * x * x + b * x + c = 0
  rw [discrim] at h
  -- âŠ¢ a * x * x + b * x + c = 0
  have ha : 2 * 2 * a â‰  0 := mul_ne_zero (mul_ne_zero (NeZero.ne _) (NeZero.ne _)) ha
  -- âŠ¢ a * x * x + b * x + c = 0
  apply mul_left_cancelâ‚€ ha
  -- âŠ¢ 2 * 2 * a * (a * x * x + b * x + c) = 2 * 2 * a * 0
  linear_combination -h
  -- ðŸŽ‰ no goals
#align quadratic_eq_zero_iff_discrim_eq_sq quadratic_eq_zero_iff_discrim_eq_sq

/-- A quadratic has no root if its discriminant has no square root. -/
theorem quadratic_ne_zero_of_discrim_ne_sq (h : âˆ€ s : R, discrim a b c â‰  s^2) (x : R) :
    a * x * x + b * x + c â‰  0 :=
  mt discrim_eq_sq_of_quadratic_eq_zero (h _)
#align quadratic_ne_zero_of_discrim_ne_sq quadratic_ne_zero_of_discrim_ne_sq

end Ring

section Field

variable {K : Type*} [Field K] [NeZero (2 : K)] {a b c x : K}

/-- Roots of a quadratic equation. -/
theorem quadratic_eq_zero_iff (ha : a â‰  0) {s : K} (h : discrim a b c = s * s) (x : K) :
    a * x * x + b * x + c = 0 â†” x = (-b + s) / (2 * a) âˆ¨ x = (-b - s) / (2 * a) := by
  rw [quadratic_eq_zero_iff_discrim_eq_sq ha, h, sq, mul_self_eq_mul_self_iff]
  -- âŠ¢ s = 2 * a * x + b âˆ¨ s = -(2 * a * x + b) â†” x = (-b + s) / (2 * a) âˆ¨ x = (-b  â€¦
  field_simp
  -- âŠ¢ s = 2 * a * x + b âˆ¨ s = -b + -(2 * a * x) â†” x * (2 * a) = -b + s âˆ¨ x * (2 *  â€¦
  apply or_congr
  -- âŠ¢ s = 2 * a * x + b â†” x * (2 * a) = -b + s
  Â· constructor <;> intro h' <;> linear_combination -h'
    -- âŠ¢ s = 2 * a * x + b â†’ x * (2 * a) = -b + s
                    -- âŠ¢ x * (2 * a) = -b + s
                    -- âŠ¢ s = 2 * a * x + b
                                 -- ðŸŽ‰ no goals
                                 -- ðŸŽ‰ no goals
  Â· constructor <;> intro h' <;> linear_combination h'
    -- âŠ¢ s = -b + -(2 * a * x) â†’ x * (2 * a) = -b - s
                    -- âŠ¢ x * (2 * a) = -b - s
                    -- âŠ¢ s = -b + -(2 * a * x)
                                 -- ðŸŽ‰ no goals
                                 -- ðŸŽ‰ no goals
#align quadratic_eq_zero_iff quadratic_eq_zero_iff

/-- A quadratic has roots if its discriminant has square roots -/
theorem exists_quadratic_eq_zero (ha : a â‰  0) (h : âˆƒ s, discrim a b c = s * s) :
    âˆƒ x, a * x * x + b * x + c = 0 := by
  rcases h with âŸ¨s, hsâŸ©
  -- âŠ¢ âˆƒ x, a * x * x + b * x + c = 0
  use (-b + s) / (2 * a)
  -- âŠ¢ a * ((-b + s) / (2 * a)) * ((-b + s) / (2 * a)) + b * ((-b + s) / (2 * a)) + â€¦
  rw [quadratic_eq_zero_iff ha hs]
  -- âŠ¢ (-b + s) / (2 * a) = (-b + s) / (2 * a) âˆ¨ (-b + s) / (2 * a) = (-b - s) / (2 â€¦
  simp
  -- ðŸŽ‰ no goals
#align exists_quadratic_eq_zero exists_quadratic_eq_zero

/-- Root of a quadratic when its discriminant equals zero -/
theorem quadratic_eq_zero_iff_of_discrim_eq_zero (ha : a â‰  0) (h : discrim a b c = 0) (x : K) :
    a * x * x + b * x + c = 0 â†” x = -b / (2 * a) := by
  have : discrim a b c = 0 * 0 := by rw [h, mul_zero]
  -- âŠ¢ a * x * x + b * x + c = 0 â†” x = -b / (2 * a)
  rw [quadratic_eq_zero_iff ha this, add_zero, sub_zero, or_self_iff]
  -- ðŸŽ‰ no goals
#align quadratic_eq_zero_iff_of_discrim_eq_zero quadratic_eq_zero_iff_of_discrim_eq_zero

end Field

section LinearOrderedField

variable {K : Type*} [LinearOrderedField K] {a b c : K}

/-- If a polynomial of degree 2 is always nonnegative, then its discriminant is nonpositive -/
theorem discrim_le_zero (h : âˆ€ x : K, 0 â‰¤ a * x * x + b * x + c) : discrim a b c â‰¤ 0 := by
  rw [discrim, sq]
  -- âŠ¢ b * b - 4 * a * c â‰¤ 0
  obtain ha | rfl | ha : a < 0 âˆ¨ a = 0 âˆ¨ 0 < a := lt_trichotomy a 0
  -- if a < 0
  Â· have : Tendsto (fun x => (a * x + b) * x + c) atTop atBot :=
      tendsto_atBot_add_const_right _ c
        ((tendsto_atBot_add_const_right _ b (tendsto_id.neg_const_mul_atTop ha)).atBot_mul_atTop
          tendsto_id)
    rcases(this.eventually (eventually_lt_atBot 0)).exists with âŸ¨x, hxâŸ©
    -- âŠ¢ b * b - 4 * a * c â‰¤ 0
    exact False.elim ((h x).not_lt <| by rwa [â† add_mul])
    -- ðŸŽ‰ no goals
  -- if a = 0
  Â· rcases eq_or_ne b 0 with (rfl | hb)
    -- âŠ¢ 0 * 0 - 4 * 0 * c â‰¤ 0
    Â· simp
      -- ðŸŽ‰ no goals
    Â· have := h ((-c - 1) / b)
      -- âŠ¢ b * b - 4 * 0 * c â‰¤ 0
      rw [mul_div_cancel' _ hb] at this
      -- âŠ¢ b * b - 4 * 0 * c â‰¤ 0
      linarith
      -- ðŸŽ‰ no goals
  -- if a > 0
  Â· have ha' : 0 â‰¤ 4 * a := mul_nonneg zero_le_four ha.le
    -- âŠ¢ b * b - 4 * a * c â‰¤ 0
    convert neg_nonpos.2 (mul_nonneg ha' (h (-b / (2 * a)))) using 1
    -- âŠ¢ b * b - 4 * a * c = -(4 * a * (a * (-b / (2 * a)) * (-b / (2 * a)) + b * (-b â€¦
    field_simp
    -- âŠ¢ (b * b - 4 * a * c) * (2 * a * (2 * a) * (2 * a)) = -(4 * a * (a * b * b * ( â€¦
    ring
    -- ðŸŽ‰ no goals
#align discrim_le_zero discrim_le_zero

lemma discrim_le_zero_of_nonpos (h : âˆ€ x : K, a * x * x + b * x + c â‰¤ 0) : discrim a b c â‰¤ 0 :=
  discrim_neg a b c â–¸ discrim_le_zero <| by simpa only [neg_mul, â† neg_add, neg_nonneg]
                                            -- ðŸŽ‰ no goals
#align discrim_le_zero_of_nonpos discrim_le_zero_of_nonpos

/-- If a polynomial of degree 2 is always positive, then its discriminant is negative,
at least when the coefficient of the quadratic term is nonzero.
-/
theorem discrim_lt_zero (ha : a â‰  0) (h : âˆ€ x : K, 0 < a * x * x + b * x + c) :
    discrim a b c < 0 := by
  have : âˆ€ x : K, 0 â‰¤ a * x * x + b * x + c := fun x => le_of_lt (h x)
  -- âŠ¢ discrim a b c < 0
  refine lt_of_le_of_ne (discrim_le_zero this) fun h' â†¦ ?_
  -- âŠ¢ False
  have := h (-b / (2 * a))
  -- âŠ¢ False
  have : a * (-b / (2 * a)) * (-b / (2 * a)) + b * (-b / (2 * a)) + c = 0 := by
    rw [quadratic_eq_zero_iff_of_discrim_eq_zero ha h' (-b / (2 * a))]
  linarith
  -- ðŸŽ‰ no goals
#align discrim_lt_zero discrim_lt_zero

lemma discrim_lt_zero_of_neg (ha : a â‰  0) (h : âˆ€ x : K, a * x * x + b * x + c < 0) :
    discrim a b c < 0 :=
  discrim_neg a b c â–¸ discrim_lt_zero (neg_ne_zero.2 ha) <| by
    simpa only [neg_mul, â† neg_add, neg_pos]
    -- ðŸŽ‰ no goals
#align discrim_lt_zero_of_neg discrim_lt_zero_of_neg

end LinearOrderedField
