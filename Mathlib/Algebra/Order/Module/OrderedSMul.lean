/-
Copyright (c) 2020 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/
import Mathlib.Algebra.Field.Defs
import Mathlib.Algebra.Group.Action.Basic
import Mathlib.Algebra.GroupWithZero.Action.Pi
import Mathlib.Algebra.GroupWithZero.Action.Prod
import Mathlib.Algebra.Order.Module.Defs

/-!
# Ordered scalar product

In this file we define

* `OrderedSMul R M` : an ordered additive commutative monoid `M` is an `OrderedSMul`
  over an `OrderedSemiring` `R` if the scalar product respects the order relation on the
  monoid and on the ring. There is a correspondence between this structure and convex cones,
  which is proven in `Mathlib/Analysis/Convex/Cone.lean`.

## Implementation notes
* We choose to define `OrderedSMul` as a `Prop`-valued mixin, so that it can be
  used for actions, modules, and algebras
  (the axioms for an "ordered algebra" are exactly that the algebra is ordered as a module).
* To get ordered modules and ordered vector spaces, it suffices to replace the
  `OrderedAddCommMonoid` and the `OrderedSemiring` as desired.

## TODO

This file is now mostly useless. We should try deleting `OrderedSMul`

## References

* https://en.wikipedia.org/wiki/Ordered_vector_space

## Tags

ordered module, ordered scalar, ordered smul, ordered action, ordered vector space
-/

/-- The ordered scalar product property is when an ordered additive commutative monoid
with a partial order has a scalar multiplication which is compatible with the order. Note that this
is different from `IsOrderedSMul`, which uses `â‰¤`, has no semiring assumption, and has no positivity
constraint on the defining conditions.
-/
class OrderedSMul (R M : Type*) [Semiring R] [PartialOrder R]
    [AddCommMonoid M] [PartialOrder M] [SMulWithZero R M] :
  Prop where
  /-- Scalar multiplication by positive elements preserves the order. -/
  protected smul_lt_smul_of_pos : âˆ€ {a b : M}, âˆ€ {c : R}, a < b â†’ 0 < c â†’ c â€¢ a < c â€¢ b
  /-- If `c â€¢ a < c â€¢ b` for some positive `c`, then `a < b`. -/
  protected lt_of_smul_lt_smul_of_pos : âˆ€ {a b : M}, âˆ€ {c : R}, c â€¢ a < c â€¢ b â†’ 0 < c â†’ a < b

variable {Î¹ ğ•œ R M N : Type*}

section OrderedSMul
variable [Semiring R] [PartialOrder R] [AddCommMonoid M] [PartialOrder M]
  [SMulWithZero R M] [OrderedSMul R M]

instance OrderedSMul.toPosSMulStrictMono : PosSMulStrictMono R M where
  smul_lt_smul_of_pos_left _a ha _bâ‚ _bâ‚‚ hb := OrderedSMul.smul_lt_smul_of_pos hb ha

instance OrderedSMul.toPosSMulReflectLT : PosSMulReflectLT R M :=
  .of_pos fun _a ha _bâ‚ _bâ‚‚ h â†¦ OrderedSMul.lt_of_smul_lt_smul_of_pos h ha

instance OrderDual.instOrderedSMul : OrderedSMul R Máµ’áµˆ where
  smul_lt_smul_of_pos := OrderedSMul.smul_lt_smul_of_pos (M := M)
  lt_of_smul_lt_smul_of_pos := OrderedSMul.lt_of_smul_lt_smul_of_pos (M := M)

end OrderedSMul

/-- To prove that a linear ordered monoid is an ordered module, it suffices to verify only the first
axiom of `OrderedSMul`. -/
theorem OrderedSMul.mk'' [Semiring ğ•œ] [PartialOrder ğ•œ]
    [AddCommMonoid M] [LinearOrder M] [SMulWithZero ğ•œ M]
    (h : âˆ€ â¦ƒc : ğ•œâ¦„, 0 < c â†’ StrictMono fun a : M => c â€¢ a) : OrderedSMul ğ•œ M :=
  { smul_lt_smul_of_pos := fun hab hc => h hc hab
    lt_of_smul_lt_smul_of_pos := fun hab hc => (h hc).lt_iff_lt.1 hab }

instance Nat.orderedSMul [AddCommMonoid M] [LinearOrder M] [IsOrderedCancelAddMonoid M] :
    OrderedSMul â„• M :=
  OrderedSMul.mk'' fun n hn a b hab => by
    cases n with
    | zero => cases hn
    | succ n =>
      induction n with
      | zero => dsimp; rwa [one_nsmul, one_nsmul]
      | succ n ih => simp only [succ_nsmul _ n.succ, _root_.add_lt_add (ih n.succ_pos) hab]

instance Int.orderedSMul [AddCommGroup M] [LinearOrder M] [IsOrderedAddMonoid M] :
    OrderedSMul â„¤ M :=
  OrderedSMul.mk'' fun n hn => by
    cases n
    Â· simp only [Int.ofNat_eq_coe, Int.natCast_pos, natCast_zsmul] at hn âŠ¢
      exact strictMono_smul_left_of_pos hn
    Â· cases (Int.negSucc_not_pos _).1 hn

section LinearOrderedSemiring
variable [Semiring R] [LinearOrder R] [IsStrictOrderedRing R]

-- TODO: `LinearOrderedField M â†’ OrderedSMul â„š M`
instance LinearOrderedSemiring.toOrderedSMul : OrderedSMul R R :=
  OrderedSMul.mk'' fun _ => strictMono_mul_left_of_pos

end LinearOrderedSemiring

section LinearOrderedSemifield

variable [Semifield ğ•œ] [PartialOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [PosMulReflectLT ğ•œ]
  [AddCommMonoid M] [PartialOrder M]
  [AddCommMonoid N] [PartialOrder N]
  [MulActionWithZero ğ•œ M] [MulActionWithZero ğ•œ N]

/-- To prove that a vector space over a linear ordered field is ordered, it suffices to verify only
the first axiom of `OrderedSMul`. -/
theorem OrderedSMul.mk' (h : âˆ€ â¦ƒa b : Mâ¦„ â¦ƒc : ğ•œâ¦„, a < b â†’ 0 < c â†’ c â€¢ a â‰¤ c â€¢ b) :
    OrderedSMul ğ•œ M := by
  have hlt' : âˆ€ (a b : M) (c : ğ•œ), a < b â†’ 0 < c â†’ c â€¢ a < c â€¢ b := by
    refine fun a b c hab hc => (h hab hc).lt_of_ne ?_
    rw [Ne, hc.ne'.isUnit.smul_left_cancel]
    exact hab.ne
  refine âŸ¨fun {a b c} => hlt' a b c, fun {a b c hab hc} => ?_âŸ©
  obtain âŸ¨c, rflâŸ© := hc.ne'.isUnit
  rw [â† inv_smul_smul c a, â† inv_smul_smul c b]
  refine hlt' _ _ _ hab (pos_of_mul_pos_right ?_ hc.le)
  simp only [c.mul_inv, zero_lt_one]

instance [OrderedSMul ğ•œ M] [OrderedSMul ğ•œ N] : OrderedSMul ğ•œ (M Ã— N) :=
  OrderedSMul.mk' fun _ _ _ h hc =>
    âŸ¨smul_le_smul_of_nonneg_left h.1.1 hc.le, smul_le_smul_of_nonneg_left h.1.2 hc.leâŸ©

instance Pi.orderedSMul {M : Î¹ â†’ Type*} [âˆ€ i, AddCommMonoid (M i)] [âˆ€ i, PartialOrder (M i)]
    [âˆ€ i, MulActionWithZero ğ•œ (M i)] [âˆ€ i, OrderedSMul ğ•œ (M i)] : OrderedSMul ğ•œ (âˆ€ i, M i) :=
  OrderedSMul.mk' fun _ _ _ h hc i => smul_le_smul_of_nonneg_left (h.le i) hc.le

end LinearOrderedSemifield
