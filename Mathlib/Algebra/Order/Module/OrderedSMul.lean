/-
Copyright (c) 2020 Fr√©d√©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fr√©d√©ric Dupuis
-/
import Mathlib.Algebra.Module.Pi
import Mathlib.Algebra.Module.Prod
import Mathlib.Algebra.Order.Group.PosPart
import Mathlib.Algebra.Order.Module.Defs
import Mathlib.Algebra.Order.Monoid.Prod
import Mathlib.Algebra.Order.Pi
import Mathlib.Data.Set.Pointwise.SMul
import Mathlib.Tactic.GCongr.Core
import Mathlib.Tactic.Positivity

#align_import algebra.order.smul from "leanprover-community/mathlib"@"3ba15165bd6927679be7c22d6091a87337e3cd0c"

/-!
# Ordered scalar product

In this file we define

* `OrderedSMul R M` : an ordered additive commutative monoid `M` is an `OrderedSMul`
  over an `OrderedSemiring` `R` if the scalar product respects the order relation on the
  monoid and on the ring. There is a correspondence between this structure and convex cones,
  which is proven in `Analysis/Convex/Cone.lean`.

## Implementation notes
* We choose to define `OrderedSMul` as a `Prop`-valued mixin, so that it can be
  used for actions, modules, and algebras
  (the axioms for an "ordered algebra" are exactly that the algebra is ordered as a module).
* To get ordered modules and ordered vector spaces, it suffices to replace the
  `OrderedAddCommMonoid` and the `OrderedSemiring` as desired.

## TODO

This file is now mostly useless. We should try deleting `OrderedSMul`

## References

* https://en.wikipedia.org/wiki/Ordered_vector_space

## Tags

ordered module, ordered scalar, ordered smul, ordered action, ordered vector space
-/

open LatticeOrderedCommGroup

/-- The ordered scalar product property is when an ordered additive commutative monoid
with a partial order has a scalar multiplication which is compatible with the order.
-/
class OrderedSMul (R M : Type*) [OrderedSemiring R] [OrderedAddCommMonoid M] [SMulWithZero R M] :
  Prop where
  /-- Scalar multiplication by positive elements preserves the order. -/
  protected smul_lt_smul_of_pos : ‚àÄ {a b : M}, ‚àÄ {c : R}, a < b ‚Üí 0 < c ‚Üí c ‚Ä¢ a < c ‚Ä¢ b
  /-- If `c ‚Ä¢ a < c ‚Ä¢ b` for some positive `c`, then `a < b`. -/
  protected lt_of_smul_lt_smul_of_pos : ‚àÄ {a b : M}, ‚àÄ {c : R}, c ‚Ä¢ a < c ‚Ä¢ b ‚Üí 0 < c ‚Üí a < b
#align ordered_smul OrderedSMul

variable {Œπ Œ± Œ≤ Œ≥ ùïú R M N : Type*}

section OrderedSMul
variable [OrderedSemiring R] [OrderedAddCommMonoid M] [SMulWithZero R M] [OrderedSMul R M]
  {s : Set M} {a b : M} {c : R}

instance OrderedSMul.toPosSMulStrictMono : PosSMulStrictMono R M where
  elim _a ha _b‚ÇÅ _b‚ÇÇ hb := OrderedSMul.smul_lt_smul_of_pos hb ha

instance OrderedSMul.toPosSMulReflectLT : PosSMulReflectLT R M :=
  PosSMulReflectLT.of_pos fun _a ha _b‚ÇÅ _b‚ÇÇ h ‚Ü¶ OrderedSMul.lt_of_smul_lt_smul_of_pos h ha

instance OrderDual.instOrderedSMul [OrderedSemiring R] [OrderedAddCommMonoid M] [SMulWithZero R M]
    [OrderedSMul R M] : OrderedSMul R M·µí·µà where
  smul_lt_smul_of_pos := OrderedSMul.smul_lt_smul_of_pos (M := M)
  lt_of_smul_lt_smul_of_pos := OrderedSMul.lt_of_smul_lt_smul_of_pos (M := M)

end OrderedSMul

/-- To prove that a linear ordered monoid is an ordered module, it suffices to verify only the first
axiom of `OrderedSMul`. -/
theorem OrderedSMul.mk'' [OrderedSemiring ùïú] [LinearOrderedAddCommMonoid M] [SMulWithZero ùïú M]
    (h : ‚àÄ ‚¶Éc : ùïú‚¶Ñ, 0 < c ‚Üí StrictMono fun a : M => c ‚Ä¢ a) : OrderedSMul ùïú M :=
  { smul_lt_smul_of_pos := fun hab hc => h hc hab
    lt_of_smul_lt_smul_of_pos := fun hab hc => (h hc).lt_iff_lt.1 hab }
#align ordered_smul.mk'' OrderedSMul.mk''

instance Nat.orderedSMul [LinearOrderedCancelAddCommMonoid M] : OrderedSMul ‚Ñï M :=
  OrderedSMul.mk'' fun n hn a b hab => by
    cases n with
    | zero => cases hn
    | succ n =>
      induction n with
      | zero => dsimp; rwa [one_nsmul, one_nsmul]
      | succ n ih => simp only [succ_nsmul _ n.succ, _root_.add_lt_add hab (ih n.succ_pos)]
#align nat.ordered_smul Nat.orderedSMul

instance Int.orderedSMul [LinearOrderedAddCommGroup M] : OrderedSMul ‚Ñ§ M :=
  OrderedSMul.mk'' fun n hn => by
    cases n
    ¬∑ simp only [Int.ofNat_eq_coe, Int.coe_nat_pos, coe_nat_zsmul] at hn ‚ä¢
      exact strictMono_smul_left_of_pos hn
    ¬∑ cases (Int.negSucc_not_pos _).1 hn
#align int.ordered_smul Int.orderedSMul

section LinearOrderedSemiring
variable [LinearOrderedSemiring R] [LinearOrderedAddCommMonoid M] [SMulWithZero R M]
  [OrderedSMul R M] {a : R}

-- TODO: `LinearOrderedField M ‚Üí OrderedSMul ‚Ñö M`
instance LinearOrderedSemiring.toOrderedSMul : OrderedSMul R R :=
  OrderedSMul.mk'' fun _ => strictMono_mul_left_of_pos
#align linear_ordered_semiring.to_ordered_smul LinearOrderedSemiring.toOrderedSMul

end LinearOrderedSemiring

section LinearOrderedSemifield

variable [LinearOrderedSemifield ùïú] [OrderedAddCommMonoid M] [OrderedAddCommMonoid N]
  [MulActionWithZero ùïú M] [MulActionWithZero ùïú N]

/-- To prove that a vector space over a linear ordered field is ordered, it suffices to verify only
the first axiom of `OrderedSMul`. -/
theorem OrderedSMul.mk' (h : ‚àÄ ‚¶Éa b : M‚¶Ñ ‚¶Éc : ùïú‚¶Ñ, a < b ‚Üí 0 < c ‚Üí c ‚Ä¢ a ‚â§ c ‚Ä¢ b) :
    OrderedSMul ùïú M := by
  have hlt' : ‚àÄ (a b : M) (c : ùïú), a < b ‚Üí 0 < c ‚Üí c ‚Ä¢ a < c ‚Ä¢ b := by
    refine' fun a b c hab hc => (h hab hc).lt_of_ne _
    rw [Ne.def, hc.ne'.isUnit.smul_left_cancel]
    exact hab.ne
  refine' { smul_lt_smul_of_pos := fun {a b c} => hlt' a b c..}
  intro a b c hab hc
  obtain ‚ü®c, rfl‚ü© := hc.ne'.isUnit
  rw [‚Üê inv_smul_smul c a, ‚Üê inv_smul_smul c b]
  refine' hlt' _ _ _ hab (pos_of_mul_pos_right _ hc.le)
  simp only [c.mul_inv, zero_lt_one]
#align ordered_smul.mk' OrderedSMul.mk'

instance [OrderedSMul ùïú M] [OrderedSMul ùïú N] : OrderedSMul ùïú (M √ó N) :=
  OrderedSMul.mk' fun _ _ _ h hc =>
    ‚ü®smul_le_smul_of_nonneg_left h.1.1 hc.le, smul_le_smul_of_nonneg_left h.1.2 hc.le‚ü©

instance Pi.orderedSMul {M : Œπ ‚Üí Type*} [‚àÄ i, OrderedAddCommMonoid (M i)]
    [‚àÄ i, MulActionWithZero ùïú (M i)] [‚àÄ i, OrderedSMul ùïú (M i)] : OrderedSMul ùïú (‚àÄ i, M i) :=
  OrderedSMul.mk' fun _ _ _ h hc i => smul_le_smul_of_nonneg_left (h.le i) hc.le
#align pi.ordered_smul Pi.orderedSMul

#noalign pi.ordered_smul'
#noalign pi.ordered_smul''

end LinearOrderedSemifield

section Invertible
variable (Œ± : Type*) {Œ≤ : Type*}
variable [Semiring Œ±] [Invertible (2 : Œ±)] [Lattice Œ≤] [AddCommGroup Œ≤] [Module Œ± Œ≤]
  [CovariantClass Œ≤ Œ≤ (¬∑ + ¬∑) (¬∑ ‚â§ ¬∑)]

lemma inf_eq_half_smul_add_sub_abs_sub (x y : Œ≤) : x ‚äì y = (‚Öü2 : Œ±) ‚Ä¢ (x + y - |y - x|) := by
  rw [‚Üê two_nsmul_inf_eq_add_sub_abs_sub x y, two_smul, ‚Üê two_smul Œ±,
    smul_smul, invOf_mul_self, one_smul]

lemma sup_eq_half_smul_add_add_abs_sub (x y : Œ≤) : x ‚äî y = (‚Öü2 : Œ±) ‚Ä¢ (x + y + |y - x|) := by
  rw [‚Üê two_nsmul_sup_eq_add_add_abs_sub x y, two_smul, ‚Üê two_smul Œ±,
    smul_smul, invOf_mul_self, one_smul]

end Invertible

section DivisionSemiring
variable (Œ± : Type*) {Œ≤ : Type*}
variable [DivisionSemiring Œ±] [NeZero (2 : Œ±)] [Lattice Œ≤] [AddCommGroup Œ≤] [Module Œ± Œ≤]
  [CovariantClass Œ≤ Œ≤ (¬∑ + ¬∑) (¬∑ ‚â§ ¬∑)]

lemma inf_eq_half_smul_add_sub_abs_sub' (x y : Œ≤) : x ‚äì y = (2‚Åª¬π : Œ±) ‚Ä¢ (x + y - |y - x|) := by
  letI := invertibleOfNonzero (two_ne_zero' Œ±)
  exact inf_eq_half_smul_add_sub_abs_sub Œ± x y

lemma sup_eq_half_smul_add_add_abs_sub' (x y : Œ≤) : x ‚äî y = (2‚Åª¬π : Œ±) ‚Ä¢ (x + y + |y - x|) := by
  letI := invertibleOfNonzero (two_ne_zero' Œ±)
  exact sup_eq_half_smul_add_add_abs_sub Œ± x y

end DivisionSemiring
