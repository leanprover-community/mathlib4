/-
Copyright (c) 2020 Fr√©d√©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fr√©d√©ric Dupuis, Ya√´l Dillies
-/
import Mathlib.Algebra.Order.SMul

#align_import algebra.order.module from "leanprover-community/mathlib"@"9003f28797c0664a49e4179487267c494477d853"

/-!
# Ordered module

In this file we provide lemmas about `OrderedSMul` that hold once a module structure is present.

## References

* https://en.wikipedia.org/wiki/Ordered_module

## Tags

ordered module, ordered scalar, ordered smul, ordered action, ordered vector space
-/


open Pointwise

variable {k M N : Type*}

instance instModuleOrderDual [Semiring k] [OrderedAddCommMonoid M] [Module k M] : Module k M·µí·µà
    where
  add_smul _ _ x := OrderDual.rec (add_smul _ _) x
  zero_smul m := OrderDual.rec (zero_smul _) m

section Semiring

variable [OrderedSemiring k] [OrderedAddCommGroup M] [Module k M] [OrderedSMul k M] {a b : M}
  {c : k}

/- Can be generalized from `Module k M` to `DistribMulActionWithZero k M` once it exists.
where `DistribMulActionWithZero k M`is the conjunction of `DistribMulAction k M` and
`SMulWithZero k M`.-/
theorem smul_neg_iff_of_pos (hc : 0 < c) : c ‚Ä¢ a < 0 ‚Üî a < 0 := by
  rw [‚Üê neg_neg a, smul_neg, neg_neg_iff_pos, neg_neg_iff_pos]
  -- ‚ä¢ 0 < c ‚Ä¢ -a ‚Üî 0 < -a
  exact smul_pos_iff_of_pos hc
  -- üéâ no goals
#align smul_neg_iff_of_pos smul_neg_iff_of_pos

end Semiring

section Ring

variable [OrderedRing k] [OrderedAddCommGroup M] [Module k M] [OrderedSMul k M] {a b : M} {c : k}

theorem smul_lt_smul_of_neg (h : a < b) (hc : c < 0) : c ‚Ä¢ b < c ‚Ä¢ a := by
  rw [‚Üê neg_neg c, neg_smul, neg_smul (-c), neg_lt_neg_iff]
  -- ‚ä¢ -c ‚Ä¢ a < -c ‚Ä¢ b
  exact smul_lt_smul_of_pos h (neg_pos_of_neg hc)
  -- üéâ no goals
#align smul_lt_smul_of_neg smul_lt_smul_of_neg

theorem smul_le_smul_of_nonpos (h : a ‚â§ b) (hc : c ‚â§ 0) : c ‚Ä¢ b ‚â§ c ‚Ä¢ a := by
  rw [‚Üê neg_neg c, neg_smul, neg_smul (-c), neg_le_neg_iff]
  -- ‚ä¢ -c ‚Ä¢ a ‚â§ -c ‚Ä¢ b
  exact smul_le_smul_of_nonneg h (neg_nonneg_of_nonpos hc)
  -- üéâ no goals
#align smul_le_smul_of_nonpos smul_le_smul_of_nonpos

theorem eq_of_smul_eq_smul_of_neg_of_le (hab : c ‚Ä¢ a = c ‚Ä¢ b) (hc : c < 0) (h : a ‚â§ b) : a = b := by
  rw [‚Üê neg_neg c, neg_smul, neg_smul (-c), neg_inj] at hab
  -- ‚ä¢ a = b
  exact eq_of_smul_eq_smul_of_pos_of_le hab (neg_pos_of_neg hc) h
  -- üéâ no goals
#align eq_of_smul_eq_smul_of_neg_of_le eq_of_smul_eq_smul_of_neg_of_le

theorem lt_of_smul_lt_smul_of_nonpos (h : c ‚Ä¢ a < c ‚Ä¢ b) (hc : c ‚â§ 0) : b < a := by
  rw [‚Üê neg_neg c, neg_smul, neg_smul (-c), neg_lt_neg_iff] at h
  -- ‚ä¢ b < a
  exact lt_of_smul_lt_smul_of_nonneg h (neg_nonneg_of_nonpos hc)
  -- üéâ no goals
#align lt_of_smul_lt_smul_of_nonpos lt_of_smul_lt_smul_of_nonpos

theorem smul_lt_smul_iff_of_neg (hc : c < 0) : c ‚Ä¢ a < c ‚Ä¢ b ‚Üî b < a := by
  rw [‚Üê neg_neg c, neg_smul, neg_smul (-c), neg_lt_neg_iff]
  -- ‚ä¢ -c ‚Ä¢ b < -c ‚Ä¢ a ‚Üî b < a
  exact smul_lt_smul_iff_of_pos (neg_pos_of_neg hc)
  -- üéâ no goals
#align smul_lt_smul_iff_of_neg smul_lt_smul_iff_of_neg

theorem smul_neg_iff_of_neg (hc : c < 0) : c ‚Ä¢ a < 0 ‚Üî 0 < a := by
  rw [‚Üê neg_neg c, neg_smul, neg_neg_iff_pos]
  -- ‚ä¢ 0 < -c ‚Ä¢ a ‚Üî 0 < a
  exact smul_pos_iff_of_pos (neg_pos_of_neg hc)
  -- üéâ no goals
#align smul_neg_iff_of_neg smul_neg_iff_of_neg

theorem smul_pos_iff_of_neg (hc : c < 0) : 0 < c ‚Ä¢ a ‚Üî a < 0 := by
  rw [‚Üê neg_neg c, neg_smul, neg_pos]
  -- ‚ä¢ -c ‚Ä¢ a < 0 ‚Üî a < 0
  exact smul_neg_iff_of_pos (neg_pos_of_neg hc)
  -- üéâ no goals
#align smul_pos_iff_of_neg smul_pos_iff_of_neg

theorem smul_nonpos_of_nonpos_of_nonneg (hc : c ‚â§ 0) (ha : 0 ‚â§ a) : c ‚Ä¢ a ‚â§ 0 :=
  calc
    c ‚Ä¢ a ‚â§ c ‚Ä¢ (0 : M) := smul_le_smul_of_nonpos ha hc
    _ = 0 := smul_zero c
#align smul_nonpos_of_nonpos_of_nonneg smul_nonpos_of_nonpos_of_nonneg

theorem smul_nonneg_of_nonpos_of_nonpos (hc : c ‚â§ 0) (ha : a ‚â§ 0) : 0 ‚â§ c ‚Ä¢ a :=
  @smul_nonpos_of_nonpos_of_nonneg k M·µí·µà _ _ _ _ _ _ hc ha
#align smul_nonneg_of_nonpos_of_nonpos smul_nonneg_of_nonpos_of_nonpos

alias ‚ü®_, smul_pos_of_neg_of_neg‚ü© := smul_pos_iff_of_neg
#align smul_pos_of_neg_of_neg smul_pos_of_neg_of_neg

alias ‚ü®_, smul_neg_of_pos_of_neg‚ü© := smul_neg_iff_of_pos
#align smul_neg_of_pos_of_neg smul_neg_of_pos_of_neg

alias ‚ü®_, smul_neg_of_neg_of_pos‚ü© := smul_neg_iff_of_neg
#align smul_neg_of_neg_of_pos smul_neg_of_neg_of_pos

theorem antitone_smul_left (hc : c ‚â§ 0) : Antitone (SMul.smul c : M ‚Üí M) := fun _ _ h =>
  smul_le_smul_of_nonpos h hc
#align antitone_smul_left antitone_smul_left

theorem strict_anti_smul_left (hc : c < 0) : StrictAnti (SMul.smul c : M ‚Üí M) := fun _ _ h =>
  smul_lt_smul_of_neg h hc
#align strict_anti_smul_left strict_anti_smul_left

/-- Binary **rearrangement inequality**. -/
theorem smul_add_smul_le_smul_add_smul [ContravariantClass M M (¬∑ + ¬∑) (¬∑ ‚â§ ¬∑)] {a b : k} {c d : M}
    (hab : a ‚â§ b) (hcd : c ‚â§ d) : a ‚Ä¢ d + b ‚Ä¢ c ‚â§ a ‚Ä¢ c + b ‚Ä¢ d := by
  obtain ‚ü®b, rfl‚ü© := exists_add_of_le hab
  -- ‚ä¢ a ‚Ä¢ d + (a + b) ‚Ä¢ c ‚â§ a ‚Ä¢ c + (a + b) ‚Ä¢ d
  obtain ‚ü®d, rfl‚ü© := exists_add_of_le hcd
  -- ‚ä¢ a ‚Ä¢ (c + d) + (a + b) ‚Ä¢ c ‚â§ a ‚Ä¢ c + (a + b) ‚Ä¢ (c + d)
  rw [smul_add, add_right_comm, smul_add, ‚Üê add_assoc, add_smul _ _ d]
  -- ‚ä¢ a ‚Ä¢ c + (a + b) ‚Ä¢ c + a ‚Ä¢ d ‚â§ a ‚Ä¢ c + (a + b) ‚Ä¢ c + (a ‚Ä¢ d + b ‚Ä¢ d)
  rw [le_add_iff_nonneg_right] at hab hcd
  -- ‚ä¢ a ‚Ä¢ c + (a + b) ‚Ä¢ c + a ‚Ä¢ d ‚â§ a ‚Ä¢ c + (a + b) ‚Ä¢ c + (a ‚Ä¢ d + b ‚Ä¢ d)
  exact add_le_add_left (le_add_of_nonneg_right <| smul_nonneg hab hcd) _
  -- üéâ no goals
#align smul_add_smul_le_smul_add_smul smul_add_smul_le_smul_add_smul

/-- Binary **rearrangement inequality**. -/
theorem smul_add_smul_le_smul_add_smul' [ContravariantClass M M (¬∑ + ¬∑) (¬∑ ‚â§ ¬∑)] {a b : k} {c d : M}
    (hba : b ‚â§ a) (hdc : d ‚â§ c) : a ‚Ä¢ d + b ‚Ä¢ c ‚â§ a ‚Ä¢ c + b ‚Ä¢ d := by
  rw [add_comm (a ‚Ä¢ d), add_comm (a ‚Ä¢ c)]
  -- ‚ä¢ b ‚Ä¢ c + a ‚Ä¢ d ‚â§ b ‚Ä¢ d + a ‚Ä¢ c
  exact smul_add_smul_le_smul_add_smul hba hdc
  -- üéâ no goals
#align smul_add_smul_le_smul_add_smul' smul_add_smul_le_smul_add_smul'

/-- Binary strict **rearrangement inequality**. -/
theorem smul_add_smul_lt_smul_add_smul [CovariantClass M M (¬∑ + ¬∑) (¬∑ < ¬∑)]
    [ContravariantClass M M (¬∑ + ¬∑) (¬∑ < ¬∑)] {a b : k} {c d : M} (hab : a < b) (hcd : c < d) :
    a ‚Ä¢ d + b ‚Ä¢ c < a ‚Ä¢ c + b ‚Ä¢ d := by
  obtain ‚ü®b, rfl‚ü© := exists_add_of_le hab.le
  -- ‚ä¢ a ‚Ä¢ d + (a + b) ‚Ä¢ c < a ‚Ä¢ c + (a + b) ‚Ä¢ d
  obtain ‚ü®d, rfl‚ü© := exists_add_of_le hcd.le
  -- ‚ä¢ a ‚Ä¢ (c + d) + (a + b) ‚Ä¢ c < a ‚Ä¢ c + (a + b) ‚Ä¢ (c + d)
  rw [smul_add, add_right_comm, smul_add, ‚Üê add_assoc, add_smul _ _ d]
  -- ‚ä¢ a ‚Ä¢ c + (a + b) ‚Ä¢ c + a ‚Ä¢ d < a ‚Ä¢ c + (a + b) ‚Ä¢ c + (a ‚Ä¢ d + b ‚Ä¢ d)
  rw [lt_add_iff_pos_right] at hab hcd
  -- ‚ä¢ a ‚Ä¢ c + (a + b) ‚Ä¢ c + a ‚Ä¢ d < a ‚Ä¢ c + (a + b) ‚Ä¢ c + (a ‚Ä¢ d + b ‚Ä¢ d)
  exact add_lt_add_left (lt_add_of_pos_right _ <| smul_pos hab hcd) _
  -- üéâ no goals
#align smul_add_smul_lt_smul_add_smul smul_add_smul_lt_smul_add_smul

/-- Binary strict **rearrangement inequality**. -/
theorem smul_add_smul_lt_smul_add_smul' [CovariantClass M M (¬∑ + ¬∑) (¬∑ < ¬∑)]
    [ContravariantClass M M (¬∑ + ¬∑) (¬∑ < ¬∑)] {a b : k} {c d : M} (hba : b < a) (hdc : d < c) :
    a ‚Ä¢ d + b ‚Ä¢ c < a ‚Ä¢ c + b ‚Ä¢ d := by
  rw [add_comm (a ‚Ä¢ d), add_comm (a ‚Ä¢ c)]
  -- ‚ä¢ b ‚Ä¢ c + a ‚Ä¢ d < b ‚Ä¢ d + a ‚Ä¢ c
  exact smul_add_smul_lt_smul_add_smul hba hdc
  -- üéâ no goals
#align smul_add_smul_lt_smul_add_smul' smul_add_smul_lt_smul_add_smul'

end Ring

section Field

variable [LinearOrderedField k] [OrderedAddCommGroup M] [Module k M] [OrderedSMul k M] {a b : M}
  {c : k}

theorem smul_le_smul_iff_of_neg (hc : c < 0) : c ‚Ä¢ a ‚â§ c ‚Ä¢ b ‚Üî b ‚â§ a := by
  rw [‚Üê neg_neg c, neg_smul, neg_smul (-c), neg_le_neg_iff]
  -- ‚ä¢ -c ‚Ä¢ b ‚â§ -c ‚Ä¢ a ‚Üî b ‚â§ a
  exact smul_le_smul_iff_of_pos (neg_pos_of_neg hc)
  -- üéâ no goals
#align smul_le_smul_iff_of_neg smul_le_smul_iff_of_neg

theorem inv_smul_le_iff_of_neg (h : c < 0) : c‚Åª¬π ‚Ä¢ a ‚â§ b ‚Üî c ‚Ä¢ b ‚â§ a := by
  rw [‚Üê smul_le_smul_iff_of_neg h, smul_inv_smul‚ÇÄ h.ne]
  -- üéâ no goals
#align inv_smul_le_iff_of_neg inv_smul_le_iff_of_neg

theorem inv_smul_lt_iff_of_neg (h : c < 0) : c‚Åª¬π ‚Ä¢ a < b ‚Üî c ‚Ä¢ b < a := by
  rw [‚Üê smul_lt_smul_iff_of_neg h, smul_inv_smul‚ÇÄ h.ne]
  -- üéâ no goals
#align inv_smul_lt_iff_of_neg inv_smul_lt_iff_of_neg

theorem smul_inv_le_iff_of_neg (h : c < 0) : a ‚â§ c‚Åª¬π ‚Ä¢ b ‚Üî b ‚â§ c ‚Ä¢ a := by
  rw [‚Üê smul_le_smul_iff_of_neg h, smul_inv_smul‚ÇÄ h.ne]
  -- üéâ no goals
#align smul_inv_le_iff_of_neg smul_inv_le_iff_of_neg

theorem smul_inv_lt_iff_of_neg (h : c < 0) : a < c‚Åª¬π ‚Ä¢ b ‚Üî b < c ‚Ä¢ a := by
  rw [‚Üê smul_lt_smul_iff_of_neg h, smul_inv_smul‚ÇÄ h.ne]
  -- üéâ no goals
#align smul_inv_lt_iff_of_neg smul_inv_lt_iff_of_neg

variable (M)

/-- Left scalar multiplication as an order isomorphism. -/
@[simps]
def OrderIso.smulLeftDual {c : k} (hc : c < 0) : M ‚âÉo M·µí·µà where
  toFun b := OrderDual.toDual (c ‚Ä¢ b)
  invFun b := c‚Åª¬π ‚Ä¢ OrderDual.ofDual b
  left_inv := inv_smul_smul‚ÇÄ hc.ne
  right_inv := smul_inv_smul‚ÇÄ hc.ne
  map_rel_iff' := (@OrderDual.toDual_le_toDual M).trans <| smul_le_smul_iff_of_neg hc
#align order_iso.smul_left_dual OrderIso.smulLeftDual

end Field

/-! ### Upper/lower bounds -/


section OrderedRing

variable [OrderedRing k] [OrderedAddCommGroup M] [Module k M] [OrderedSMul k M] {s : Set M} {c : k}

theorem smul_lowerBounds_subset_upperBounds_smul (hc : c ‚â§ 0) :
    c ‚Ä¢ lowerBounds s ‚äÜ upperBounds (c ‚Ä¢ s) :=
  (antitone_smul_left hc).image_lowerBounds_subset_upperBounds_image
#align smul_lower_bounds_subset_upper_bounds_smul smul_lowerBounds_subset_upperBounds_smul

theorem smul_upperBounds_subset_lowerBounds_smul (hc : c ‚â§ 0) :
    c ‚Ä¢ upperBounds s ‚äÜ lowerBounds (c ‚Ä¢ s) :=
  (antitone_smul_left hc).image_upperBounds_subset_lowerBounds_image
#align smul_upper_bounds_subset_lower_bounds_smul smul_upperBounds_subset_lowerBounds_smul

theorem BddBelow.smul_of_nonpos (hc : c ‚â§ 0) (hs : BddBelow s) : BddAbove (c ‚Ä¢ s) :=
  (antitone_smul_left hc).map_bddBelow hs
#align bdd_below.smul_of_nonpos BddBelow.smul_of_nonpos

theorem BddAbove.smul_of_nonpos (hc : c ‚â§ 0) (hs : BddAbove s) : BddBelow (c ‚Ä¢ s) :=
  (antitone_smul_left hc).map_bddAbove hs
#align bdd_above.smul_of_nonpos BddAbove.smul_of_nonpos

end OrderedRing

section LinearOrderedField

variable [LinearOrderedField k] [OrderedAddCommGroup M] [Module k M] [OrderedSMul k M] {s : Set M}
  {c : k}

@[simp]
theorem lowerBounds_smul_of_neg (hc : c < 0) : lowerBounds (c ‚Ä¢ s) = c ‚Ä¢ upperBounds s :=
  (OrderIso.smulLeftDual M hc).upperBounds_image
#align lower_bounds_smul_of_neg lowerBounds_smul_of_neg

@[simp]
theorem upperBounds_smul_of_neg (hc : c < 0) : upperBounds (c ‚Ä¢ s) = c ‚Ä¢ lowerBounds s :=
  (OrderIso.smulLeftDual M hc).lowerBounds_image
#align upper_bounds_smul_of_neg upperBounds_smul_of_neg

@[simp]
theorem bddBelow_smul_iff_of_neg (hc : c < 0) : BddBelow (c ‚Ä¢ s) ‚Üî BddAbove s :=
  (OrderIso.smulLeftDual M hc).bddAbove_image
#align bdd_below_smul_iff_of_neg bddBelow_smul_iff_of_neg

@[simp]
theorem bddAbove_smul_iff_of_neg (hc : c < 0) : BddAbove (c ‚Ä¢ s) ‚Üî BddBelow s :=
  (OrderIso.smulLeftDual M hc).bddBelow_image
#align bdd_above_smul_iff_of_neg bddAbove_smul_iff_of_neg

end LinearOrderedField
