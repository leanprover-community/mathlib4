/-
Copyright (c) 2024 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Algebra.BigOperators.Expect
import Mathlib.Algebra.Module.Rat
import Mathlib.Algebra.Order.BigOperators.Ring.Finset
import Mathlib.Algebra.Order.Module.Field
import Mathlib.Algebra.Order.Module.Rat
import Mathlib.Tactic.GCongr

/-!
# Order properties of the average over a finset
-/

open Function
open Fintype (card)
open scoped BigOperators Pointwise NNRat

variable {Î¹ Î± R : Type*}

local notation a " /â„š " q => (q : â„šâ‰¥0)â»Â¹ â€¢ a

namespace Finset
section OrderedAddCommMonoid
variable [AddCommMonoid Î±] [PartialOrder Î±] [IsOrderedAddMonoid Î±] [Module â„šâ‰¥0 Î±]
  {s : Finset Î¹} {f g : Î¹ â†’ Î±}

lemma expect_eq_zero_iff_of_nonneg (hs : s.Nonempty) (hf : âˆ€ i âˆˆ s, 0 â‰¤ f i) :
    ğ”¼ i âˆˆ s, f i = 0 â†” âˆ€ i âˆˆ s, f i = 0 := by
  simp [expect, sum_eq_zero_iff_of_nonneg hf, hs.ne_empty]

lemma expect_eq_zero_iff_of_nonpos (hs : s.Nonempty) (hf : âˆ€ i âˆˆ s, f i â‰¤ 0) :
    ğ”¼ i âˆˆ s, f i = 0 â†” âˆ€ i âˆˆ s, f i = 0 := by
  simp [expect, sum_eq_zero_iff_of_nonpos hf, hs.ne_empty]

section PosSMulMono
variable [PosSMulMono â„šâ‰¥0 Î±] {a : Î±}

lemma expect_le_expect (hfg : âˆ€ i âˆˆ s, f i â‰¤ g i) : ğ”¼ i âˆˆ s, f i â‰¤ ğ”¼ i âˆˆ s, g i :=
  smul_le_smul_of_nonneg_left (sum_le_sum hfg) <| by positivity

/-- This is a (beta-reduced) version of the standard lemma `Finset.expect_le_expect`,
convenient for the `gcongr` tactic. -/
@[gcongr]
lemma _root_.GCongr.expect_le_expect (h : âˆ€ i âˆˆ s, f i â‰¤ g i) : s.expect f â‰¤ s.expect g :=
  Finset.expect_le_expect h

lemma expect_le (hs : s.Nonempty) (h : âˆ€ x âˆˆ s, f x â‰¤ a) : ğ”¼ i âˆˆ s, f i â‰¤ a :=
  (inv_smul_le_iff_of_pos <| mod_cast hs.card_pos).2 <| by
    rw [Nat.cast_smul_eq_nsmul]; exact sum_le_card_nsmul _ _ _ h

lemma le_expect (hs : s.Nonempty) (h : âˆ€ x âˆˆ s, a â‰¤ f x) : a â‰¤ ğ”¼ i âˆˆ s, f i :=
  (le_inv_smul_iff_of_pos <| mod_cast hs.card_pos).2 <| by
    rw [Nat.cast_smul_eq_nsmul]; exact card_nsmul_le_sum _ _ _ h

lemma expect_nonneg (hf : âˆ€ i âˆˆ s, 0 â‰¤ f i) : 0 â‰¤ ğ”¼ i âˆˆ s, f i :=
  smul_nonneg (by positivity) <| sum_nonneg hf

end PosSMulMono

section PosSMulMono
variable {M N : Type*} [AddCommMonoid M] [Module â„šâ‰¥0 M]
  [AddCommMonoid N] [PartialOrder N] [IsOrderedAddMonoid N] [Module â„šâ‰¥0 N]
  [PosSMulMono â„šâ‰¥0 N] {m : M â†’ N} {p : M â†’ Prop} {f : Î¹ â†’ M} {s : Finset Î¹}

/-- Let `{a | p a}` be an additive subsemigroup of an additive commutative monoid `M`. If `m` is a
subadditive function (`m (a + b) â‰¤ m a + m b`) preserved under division by a natural, `f` is a
function valued in that subsemigroup and `s` is a nonempty set, then
`m (ğ”¼ i âˆˆ s, f i) â‰¤ ğ”¼ i âˆˆ s, m (f i)`. -/
lemma le_expect_nonempty_of_subadditive_on_pred (h_add : âˆ€ a b, p a â†’ p b â†’ m (a + b) â‰¤ m a + m b)
    (hp_add : âˆ€ a b, p a â†’ p b â†’ p (a + b)) (h_div : âˆ€ (n : â„•) a, p a â†’ m (a /â„š n) = m a /â„š n)
    (hs_nonempty : s.Nonempty) (hs : âˆ€ i âˆˆ s, p (f i)) :
    m (ğ”¼ i âˆˆ s, f i) â‰¤ ğ”¼ i âˆˆ s, m (f i) := by
  simp only [expect, h_div _ _ (sum_induction_nonempty _ _ hp_add hs_nonempty hs)]
  exact smul_le_smul_of_nonneg_left
    (le_sum_nonempty_of_subadditive_on_pred _ _ h_add hp_add _ _ hs_nonempty hs) <| by positivity

/-- If `m : M â†’ N` is a subadditive function (`m (a + b) â‰¤ m a + m b`) and `s` is a nonempty set,
then `m (ğ”¼ i âˆˆ s, f i) â‰¤ ğ”¼ i âˆˆ s, m (f i)`. -/
lemma le_expect_nonempty_of_subadditive (m : M â†’ N) (h_mul : âˆ€ a b, m (a + b) â‰¤ m a + m b)
    (h_div : âˆ€ (n : â„•) a, m (a /â„š n) = m a /â„š n) (hs : s.Nonempty) :
    m (ğ”¼ i âˆˆ s, f i) â‰¤ ğ”¼ i âˆˆ s, m (f i) :=
  le_expect_nonempty_of_subadditive_on_pred (p := fun _ â†¦ True) (by simpa) (by simp) (by simpa) hs
    (by simp)

/-- Let `{a | p a}` be a subsemigroup of a commutative monoid `M`. If `m` is a subadditive function
(`m (x + y) â‰¤ m x + m y`, `m 0 = 0`) preserved under division by a natural and `f` is a function
valued in that subsemigroup, then `m (ğ”¼ i âˆˆ s, f i) â‰¤ ğ”¼ i âˆˆ s, m (f i)`. -/
lemma le_expect_of_subadditive_on_pred (h_zero : m 0 = 0)
    (h_add : âˆ€ a b, p a â†’ p b â†’ m (a + b) â‰¤ m a + m b) (hp_add : âˆ€ a b, p a â†’ p b â†’ p (a + b))
    (h_div : âˆ€ (n : â„•) a, p a â†’ m (a /â„š n) = m a /â„š n)
    (hs : âˆ€ i âˆˆ s, p (f i)) : m (ğ”¼ i âˆˆ s, f i) â‰¤ ğ”¼ i âˆˆ s, m (f i) := by
  obtain rfl | hs_nonempty := s.eq_empty_or_nonempty
  Â· simp [h_zero]
  Â· exact le_expect_nonempty_of_subadditive_on_pred h_add hp_add h_div hs_nonempty hs

-- TODO: Contribute back better docstring to `le_prod_of_submultiplicative`
/-- If `m` is a subadditive function (`m (x + y) â‰¤ m x + m y`, `m 0 = 0`) preserved under division
by a natural, then `m (ğ”¼ i âˆˆ s, f i) â‰¤ ğ”¼ i âˆˆ s, m (f i)`. -/
lemma le_expect_of_subadditive (h_zero : m 0 = 0) (h_add : âˆ€ a b, m (a + b) â‰¤ m a + m b)
    (h_div : âˆ€ (n : â„•) a, m (a /â„š n) = m a /â„š n) : m (ğ”¼ i âˆˆ s, f i) â‰¤ ğ”¼ i âˆˆ s, m (f i) :=
  le_expect_of_subadditive_on_pred (p := fun _ â†¦ True) h_zero (by simpa) (by simp) (by simpa)
    (by simp)

end PosSMulMono
end OrderedAddCommMonoid

section OrderedCancelAddCommMonoid
variable [AddCommMonoid Î±] [PartialOrder Î±] [IsOrderedCancelAddMonoid Î±] [Module â„šâ‰¥0 Î±]
  {s : Finset Î¹} {f : Î¹ â†’ Î±}
section PosSMulStrictMono
variable [PosSMulStrictMono â„šâ‰¥0 Î±]

lemma expect_pos (hf : âˆ€ i âˆˆ s, 0 < f i) (hs : s.Nonempty) : 0 < ğ”¼ i âˆˆ s, f i :=
  smul_pos (inv_pos.2 <| mod_cast hs.card_pos) <| sum_pos hf hs

end PosSMulStrictMono
end OrderedCancelAddCommMonoid

section LinearOrderedAddCommMonoid
variable [AddCommMonoid Î±] [LinearOrder Î±] [IsOrderedAddMonoid Î±] [Module â„šâ‰¥0 Î±]
  [PosSMulMono â„šâ‰¥0 Î±] {s : Finset Î¹}
  {f : Î¹ â†’ Î±} {a : Î±}

lemma exists_lt_of_lt_expect (hs : s.Nonempty) (h : a < ğ”¼ i âˆˆ s, f i) : âˆƒ x âˆˆ s, a < f x := by
  contrapose! h; exact expect_le hs h

lemma exists_lt_of_expect_lt (hs : s.Nonempty) (h : ğ”¼ i âˆˆ s, f i < a) : âˆƒ x âˆˆ s, f x < a := by
  contrapose! h; exact le_expect hs h

end LinearOrderedAddCommMonoid

section LinearOrderedAddCommGroup
variable [AddCommGroup Î±] [LinearOrder Î±] [IsOrderedAddMonoid Î±] [Module â„šâ‰¥0 Î±] [PosSMulMono â„šâ‰¥0 Î±]

lemma abs_expect_le (s : Finset Î¹) (f : Î¹ â†’ Î±) : |ğ”¼ i âˆˆ s, f i| â‰¤ ğ”¼ i âˆˆ s, |f i| :=
  le_expect_of_subadditive abs_zero abs_add (fun _ â†¦ abs_nnqsmul _)

end LinearOrderedAddCommGroup

section LinearOrderedCommSemiring
variable [CommSemiring R] [LinearOrder R] [IsStrictOrderedRing R] [ExistsAddOfLE R] [Module â„šâ‰¥0 R]
  [PosSMulMono â„šâ‰¥0 R]

/-- **Cauchy-Schwarz inequality** in terms of `Finset.expect`. -/
lemma expect_mul_sq_le_sq_mul_sq (s : Finset Î¹) (f g : Î¹ â†’ R) :
    (ğ”¼ i âˆˆ s, f i * g i) ^ 2 â‰¤ (ğ”¼ i âˆˆ s, f i ^ 2) * ğ”¼ i âˆˆ s, g i ^ 2 := by
  simp only [expect, smul_pow, inv_pow, smul_mul_smul_comm, â† sq]
  gcongr
  exact sum_mul_sq_le_sq_mul_sq ..

end LinearOrderedCommSemiring
end Finset

open Finset

namespace Fintype
variable [Fintype Î¹]

section OrderedAddCommMonoid
variable [AddCommMonoid Î±] [PartialOrder Î±] [IsOrderedAddMonoid Î±] [Module â„šâ‰¥0 Î±] {f : Î¹ â†’ Î±}

lemma expect_eq_zero_iff_of_nonneg [Nonempty Î¹] (hf : 0 â‰¤ f) : ğ”¼ i, f i = 0 â†” f = 0 := by
  simp [expect, sum_eq_zero_iff_of_nonneg hf, univ_nonempty.ne_empty]

lemma expect_eq_zero_iff_of_nonpos [Nonempty Î¹] (hf : f â‰¤ 0) : ğ”¼ i, f i = 0 â†” f = 0 := by
  simp [expect, sum_eq_zero_iff_of_nonpos hf, univ_nonempty.ne_empty]

end OrderedAddCommMonoid
end Fintype

open Finset

namespace Mathlib.Meta.Positivity
open Qq Lean Meta Finset
open scoped BigOperators

attribute [local instance] monadLiftOptionMetaM in
/-- Positivity extension for `Finset.expect`. -/
@[positivity Finset.expect _ _]
def evalFinsetExpect : PositivityExt where eval {u Î±} zÎ± pÎ± e := do
  match e with
  | ~q(@Finset.expect $Î¹ _ $instÎ± $instmod $s $f) =>
    let i : Q($Î¹) â† mkFreshExprMVarQ q($Î¹) .syntheticOpaque
    have body : Q($Î±) := .betaRev f #[i]
    let rbody â† core zÎ± pÎ± body
    let p_pos : Option Q(0 < $e) := â† (do
      let .positive pbody := rbody | pure none -- Fail if the body is not provably positive
      let .some ps â† proveFinsetNonempty s | pure none
      let .some pÎ±' â† trySynthInstanceQ q(IsOrderedCancelAddMonoid $Î±) | pure none
      let .some instÎ±ordsmul â† trySynthInstanceQ q(PosSMulStrictMono â„šâ‰¥0 $Î±) | pure none
      assumeInstancesCommute
      let pr : Q(âˆ€ i, 0 < $f i) â† mkLambdaFVars #[i] pbody
      return some
        q(@expect_pos $Î¹ $Î± $instÎ± $pÎ± $pÎ±' $instmod $s $f $instÎ±ordsmul (fun i _ â†¦ $pr i) $ps))
    -- Try to show that the sum is positive
    if let some p_pos := p_pos then
      return .positive p_pos
    -- Fall back to showing that the sum is nonnegative
    else
      let pbody â† rbody.toNonneg
      let pr : Q(âˆ€ i, 0 â‰¤ $f i) â† mkLambdaFVars #[i] pbody
      let instÎ±ordmon â† synthInstanceQ q(IsOrderedAddMonoid $Î±)
      let instÎ±ordsmul â† synthInstanceQ q(PosSMulMono â„šâ‰¥0 $Î±)
      assumeInstancesCommute
      return .nonnegative
        q(@expect_nonneg $Î¹ $Î± $instÎ± $pÎ± $instÎ±ordmon $instmod $s $f $instÎ±ordsmul fun i _ â†¦ $pr i)
  | _ => throwError "not Finset.expect"

example (n : â„•) (a : â„• â†’ â„š) : 0 â‰¤ ğ”¼ j âˆˆ range n, a j^2 := by positivity
example (a : ULift.{2} â„• â†’ â„š) (s : Finset (ULift.{2} â„•)) : 0 â‰¤ ğ”¼ j âˆˆ s, a j^2 := by positivity
example (n : â„•) (a : â„• â†’ â„š) : 0 â‰¤ ğ”¼ j : Fin 8, ğ”¼ i âˆˆ range n, (a j^2 + i ^ 2) := by positivity
example (n : â„•) (a : â„• â†’ â„š) : 0 < ğ”¼ j : Fin (n + 1), (a j^2 + 1) := by positivity
example (a : â„• â†’ â„š) : 0 < ğ”¼ j âˆˆ ({1} : Finset â„•), (a j^2 + 1) := by positivity

end Mathlib.Meta.Positivity
