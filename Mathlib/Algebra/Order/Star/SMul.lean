/-
Copyright (c) 2025 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/

import Mathlib.Algebra.Order.Star.Basic
import Mathlib.Algebra.Order.Module.OrderedSMul
import Mathlib.Analysis.RCLike.Basic
import Mathlib.Data.NNReal.Star

/-!
# `StarOrderedRing` implies `OrderedSMul`

We show that `StarOrderedRing R` implies `OrderedSMul ğ•œ R` when `ğ•œ` is either `RCLike` or
`ğ•œ = â„â‰¥0`.
-/

open scoped ComplexOrder

namespace StarOrderedRing

section RCLike

variable {ğ•œ R : Type*} [RCLike ğ•œ] [NonUnitalRing R] [PartialOrder R]
    [StarRing R] [StarOrderedRing R] [Module ğ•œ R] [StarModule ğ•œ R]
    [IsScalarTower ğ•œ R R] [SMulCommClass ğ•œ R R]

open AddSubmonoid in
lemma smul_nonneg {c : ğ•œ} {a : R} (hc : 0 â‰¤ c) (ha : 0 â‰¤ a) : 0 â‰¤ c â€¢ a := by
  rw [nonneg_iff] at ha
  induction ha using closure_induction with
  | mem x hx =>
      obtain âŸ¨rc, hrcâ‚, rflâŸ© := RCLike.nonneg_iff_exists_ofReal.mp hc
      obtain âŸ¨z, rflâŸ© := hx
      let y := (Real.sqrt rc : ğ•œ) â€¢ z
      have : (Real.sqrt rc : ğ•œ) * Real.sqrt rc = rc := by exact_mod_cast Real.mul_self_sqrt hrcâ‚
      have hmain : (rc : ğ•œ) â€¢ (star z * z) = star y * y := by
        simp [y, smul_mul_smul, this]
      rw [hmain]
      exact star_mul_self_nonneg y
  | one => simp
  | mul x y hx hy hx' hy' => simp [Left.add_nonneg hx' hy']

lemma smul_lt_smul_of_pos {a b : R} {c : ğ•œ} (hab : a < b) (hc : 0 < c) : c â€¢ a < c â€¢ b :=
  have hab' : 0 < b - a := sub_pos_of_lt hab
  lt_of_sub_pos <| smul_sub c b a â–¸
    lt_of_le_of_ne (smul_nonneg (le_of_lt hc) (le_of_lt hab'))
    (smul_ne_zero (ne_of_lt hc).symm (ne_of_lt hab').symm).symm

instance (priority := 100) toOrderedSMulRCLike : OrderedSMul ğ•œ R where
  smul_lt_smul_of_pos := smul_lt_smul_of_pos
  lt_of_smul_lt_smul_of_pos {a} {b} {c} hab hc := by
    have hc' : câ»Â¹ * c = 1 := by grind
    have hmain : câ»Â¹ â€¢ (c â€¢ a) < câ»Â¹ â€¢ (c â€¢ b) := smul_lt_smul_of_pos hab (inv_pos_of_pos hc)
    simpa [smul_smul, hc'] using hmain

end RCLike

section NNReal
open scoped NNReal

variable {R : Type*} [NonUnitalRing R] [PartialOrder R]
    [StarRing R] [StarOrderedRing R] [Module â„â‰¥0 R] [StarModule â„â‰¥0 R]
    [IsScalarTower â„â‰¥0 R R] [SMulCommClass â„â‰¥0 R R]

open AddSubmonoid in
lemma smul_nnreal_nonneg {c : â„â‰¥0} {a : R} (ha : 0 â‰¤ a) : 0 â‰¤ c â€¢ a := by
  rw [nonneg_iff] at ha
  induction ha using closure_induction with
  | mem x hx =>
      obtain âŸ¨z, rflâŸ© := hx
      let y := NNReal.sqrt c â€¢ z
      have hmain : c â€¢ (star z * z) = star y * y := by
        simp [y, smul_mul_smul, NNReal.mul_self_sqrt]
      rw [hmain]
      exact star_mul_self_nonneg y
  | one => simp
  | mul x y hx hy hx' hy' => simp [Left.add_nonneg hx' hy']

lemma smul_lt_smul_of_pos_nnreal {a b : R} {c : â„â‰¥0} (hab : a < b) (hc : 0 < c) :
    c â€¢ a < c â€¢ b :=
  have hab' : 0 < b - a := sub_pos_of_lt hab
  lt_of_sub_pos <| smul_sub c b a â–¸
    lt_of_le_of_ne (smul_nnreal_nonneg (le_of_lt hab'))
    (smul_ne_zero (ne_of_lt hc).symm (ne_of_lt hab').symm).symm

instance (priority := 100) toOrderedSMulNNReal : OrderedSMul â„â‰¥0 R where
  smul_lt_smul_of_pos := smul_lt_smul_of_pos_nnreal
  lt_of_smul_lt_smul_of_pos {a} {b} {c} hab hc := by
    have hc' : câ»Â¹ * c = 1 := inv_mul_cancelâ‚€ (pos_iff_ne_zero.mp hc)
    have hmain : câ»Â¹ â€¢ (c â€¢ a) < câ»Â¹ â€¢ (c â€¢ b) := smul_lt_smul_of_pos_nnreal hab (inv_pos_of_pos hc)
    simpa [smul_smul, hc'] using hmain

end NNReal

end StarOrderedRing
