/-
Copyright (c) 2018 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro
-/
import Mathlib.Data.Int.LeastGreatest
import Mathlib.Data.Rat.Floor
import Mathlib.Algebra.Order.Field.Power

#align_import algebra.order.archimedean from "leanprover-community/mathlib"@"6f413f3f7330b94c92a5a27488fdc74e6d483a78"

/-!
# Archimedean groups and fields.

This file defines the archimedean property for ordered groups and proves several results connected
to this notion. Being archimedean means that for all elements `x` and `y>0` there exists a natural
number `n` such that `x ‚â§ n ‚Ä¢ y`.

## Main definitions

* `Archimedean` is a typeclass for an ordered additive commutative monoid to have the archimedean
  property.
* `Archimedean.floorRing` defines a floor function on an archimedean linearly ordered ring making
  it into a `floorRing`.

## Main statements

* `‚Ñï`, `‚Ñ§`, and `‚Ñö` are archimedean.
-/

set_option autoImplicit true

open Int Set

variable {Œ± : Type*}

/-- An ordered additive commutative monoid is called `Archimedean` if for any two elements `x`, `y`
such that `0 < y`, there exists a natural number `n` such that `x ‚â§ n ‚Ä¢ y`. -/
class Archimedean (Œ±) [OrderedAddCommMonoid Œ±] : Prop where
  /-- For any two elements `x`, `y` such that `0 < y`, there exists a natural number `n`
  such that `x ‚â§ n ‚Ä¢ y`. -/
  arch : ‚àÄ (x : Œ±) {y : Œ±}, 0 < y ‚Üí ‚àÉ n : ‚Ñï, x ‚â§ n ‚Ä¢ y
#align archimedean Archimedean

instance OrderDual.archimedean [OrderedAddCommGroup Œ±] [Archimedean Œ±] : Archimedean Œ±·µí·µà :=
  ‚ü®fun x y hy =>
    let ‚ü®n, hn‚ü© := Archimedean.arch (-ofDual x) (neg_pos.2 hy)
    ‚ü®n, by rwa [neg_nsmul, neg_le_neg_iff] at hn‚ü©‚ü©
           -- üéâ no goals
#align order_dual.archimedean OrderDual.archimedean

theorem exists_lt_nsmul [OrderedAddCommMonoid M] [Archimedean M]
    [CovariantClass M M (¬∑ + ¬∑) (¬∑ < ¬∑)] {a : M} (ha : 0 < a) (b : M) :
    ‚àÉ n : ‚Ñï, b < n ‚Ä¢ a :=
  let ‚ü®k, hk‚ü© := Archimedean.arch b ha; ‚ü®k + 1, hk.trans_lt <| nsmul_lt_nsmul ha k.lt_succ_self‚ü©

section LinearOrderedAddCommGroup

variable [LinearOrderedAddCommGroup Œ±] [Archimedean Œ±]

/-- An archimedean decidable linearly ordered `AddCommGroup` has a version of the floor: for
`a > 0`, any `g` in the group lies between some two consecutive multiples of `a`. -/
theorem existsUnique_zsmul_near_of_pos {a : Œ±} (ha : 0 < a) (g : Œ±) :
    ‚àÉ! k : ‚Ñ§, k ‚Ä¢ a ‚â§ g ‚àß g < (k + 1) ‚Ä¢ a := by
  let s : Set ‚Ñ§ := { n : ‚Ñ§ | n ‚Ä¢ a ‚â§ g }
  -- ‚ä¢ ‚àÉ! k, k ‚Ä¢ a ‚â§ g ‚àß g < (k + 1) ‚Ä¢ a
  obtain ‚ü®k, hk : -g ‚â§ k ‚Ä¢ a‚ü© := Archimedean.arch (-g) ha
  -- ‚ä¢ ‚àÉ! k, k ‚Ä¢ a ‚â§ g ‚àß g < (k + 1) ‚Ä¢ a
  have h_ne : s.Nonempty := ‚ü®-k, by simpa using neg_le_neg hk‚ü©
  -- ‚ä¢ ‚àÉ! k, k ‚Ä¢ a ‚â§ g ‚àß g < (k + 1) ‚Ä¢ a
  obtain ‚ü®k, hk‚ü© := Archimedean.arch g ha
  -- ‚ä¢ ‚àÉ! k, k ‚Ä¢ a ‚â§ g ‚àß g < (k + 1) ‚Ä¢ a
  have h_bdd : ‚àÄ n ‚àà s, n ‚â§ (k : ‚Ñ§) := by
    intro n hn
    apply (zsmul_le_zsmul_iff ha).mp
    rw [‚Üê coe_nat_zsmul] at hk
    exact le_trans hn hk
  obtain ‚ü®m, hm, hm'‚ü© := Int.exists_greatest_of_bdd ‚ü®k, h_bdd‚ü© h_ne
  -- ‚ä¢ ‚àÉ! k, k ‚Ä¢ a ‚â§ g ‚àß g < (k + 1) ‚Ä¢ a
  have hm'' : g < (m + 1) ‚Ä¢ a := by
    contrapose! hm'
    exact ‚ü®m + 1, hm', lt_add_one _‚ü©
  refine' ‚ü®m, ‚ü®hm, hm''‚ü©, fun n hn => (hm' n hn.1).antisymm <| Int.le_of_lt_add_one _‚ü©
  -- ‚ä¢ m < n + 1
  rw [‚Üê zsmul_lt_zsmul_iff ha]
  -- ‚ä¢ m ‚Ä¢ a < (n + 1) ‚Ä¢ a
  exact lt_of_le_of_lt hm hn.2
  -- üéâ no goals
#align exists_unique_zsmul_near_of_pos existsUnique_zsmul_near_of_pos

theorem existsUnique_zsmul_near_of_pos' {a : Œ±} (ha : 0 < a) (g : Œ±) :
    ‚àÉ! k : ‚Ñ§, 0 ‚â§ g - k ‚Ä¢ a ‚àß g - k ‚Ä¢ a < a := by
  simpa only [sub_nonneg, add_zsmul, one_zsmul, sub_lt_iff_lt_add'] using
    existsUnique_zsmul_near_of_pos ha g
#align exists_unique_zsmul_near_of_pos' existsUnique_zsmul_near_of_pos'

theorem existsUnique_sub_zsmul_mem_Ico {a : Œ±} (ha : 0 < a) (b c : Œ±) :
    ‚àÉ! m : ‚Ñ§, b - m ‚Ä¢ a ‚àà Set.Ico c (c + a) := by
  simpa only [mem_Ico, le_sub_iff_add_le, zero_add, add_comm c, sub_lt_iff_lt_add', add_assoc] using
    existsUnique_zsmul_near_of_pos' ha (b - c)
#align exists_unique_sub_zsmul_mem_Ico existsUnique_sub_zsmul_mem_Ico

theorem existsUnique_add_zsmul_mem_Ico {a : Œ±} (ha : 0 < a) (b c : Œ±) :
    ‚àÉ! m : ‚Ñ§, b + m ‚Ä¢ a ‚àà Set.Ico c (c + a) :=
  (Equiv.neg ‚Ñ§).bijective.existsUnique_iff.2 <| by
    simpa only [Equiv.neg_apply, mem_Ico, neg_zsmul, ‚Üê sub_eq_add_neg, le_sub_iff_add_le, zero_add,
      add_comm c, sub_lt_iff_lt_add', add_assoc] using existsUnique_zsmul_near_of_pos' ha (b - c)
#align exists_unique_add_zsmul_mem_Ico existsUnique_add_zsmul_mem_Ico

theorem existsUnique_add_zsmul_mem_Ioc {a : Œ±} (ha : 0 < a) (b c : Œ±) :
    ‚àÉ! m : ‚Ñ§, b + m ‚Ä¢ a ‚àà Set.Ioc c (c + a) :=
  (Equiv.addRight (1 : ‚Ñ§)).bijective.existsUnique_iff.2 <| by
    simpa only [add_zsmul, sub_lt_iff_lt_add', le_sub_iff_add_le', ‚Üê add_assoc, and_comm, mem_Ioc,
      Equiv.coe_addRight, one_zsmul, add_le_add_iff_right] using
      existsUnique_zsmul_near_of_pos ha (c - b)
#align exists_unique_add_zsmul_mem_Ioc existsUnique_add_zsmul_mem_Ioc

theorem existsUnique_sub_zsmul_mem_Ioc {a : Œ±} (ha : 0 < a) (b c : Œ±) :
    ‚àÉ! m : ‚Ñ§, b - m ‚Ä¢ a ‚àà Set.Ioc c (c + a) :=
  (Equiv.neg ‚Ñ§).bijective.existsUnique_iff.2 <| by
    simpa only [Equiv.neg_apply, neg_zsmul, sub_neg_eq_add] using
      existsUnique_add_zsmul_mem_Ioc ha b c
#align exists_unique_sub_zsmul_mem_Ioc existsUnique_sub_zsmul_mem_Ioc

end LinearOrderedAddCommGroup

theorem exists_nat_gt [StrictOrderedSemiring Œ±] [Archimedean Œ±] (x : Œ±) : ‚àÉ n : ‚Ñï, x < n :=
  (exists_lt_nsmul zero_lt_one x).imp fun n hn ‚Ü¶ by rwa [‚Üê nsmul_one]
                                                    -- üéâ no goals
#align exists_nat_gt exists_nat_gt

theorem exists_nat_ge [OrderedSemiring Œ±] [Archimedean Œ±] (x : Œ±) : ‚àÉ n : ‚Ñï, x ‚â§ n := by
  nontriviality Œ±
  -- ‚ä¢ ‚àÉ n, x ‚â§ ‚Üën
  exact (Archimedean.arch x one_pos).imp fun n h => by rwa [‚Üê nsmul_one]
  -- üéâ no goals
#align exists_nat_ge exists_nat_ge

theorem add_one_pow_unbounded_of_pos [StrictOrderedSemiring Œ±] [Archimedean Œ±] (x : Œ±) {y : Œ±}
    (hy : 0 < y) : ‚àÉ n : ‚Ñï, x < (y + 1) ^ n :=
  have : 0 ‚â§ 1 + y := add_nonneg zero_le_one hy.le
  (Archimedean.arch x hy).imp fun n h ‚Ü¶
    calc
      x ‚â§ n ‚Ä¢ y := h
      _ = n * y := nsmul_eq_mul _ _
      _ < 1 + n * y := lt_one_add _
      _ ‚â§ (1 + y) ^ n :=
        one_add_mul_le_pow' (mul_nonneg hy.le hy.le) (mul_nonneg this this)
          (add_nonneg zero_le_two hy.le) _
      _ = (y + 1) ^ n := by rw [add_comm]
                            -- üéâ no goals
#align add_one_pow_unbounded_of_pos add_one_pow_unbounded_of_pos

section StrictOrderedRing

variable [StrictOrderedRing Œ±] [Archimedean Œ±]

theorem pow_unbounded_of_one_lt (x : Œ±) {y : Œ±} (hy1 : 1 < y) : ‚àÉ n : ‚Ñï, x < y ^ n :=
  sub_add_cancel y 1 ‚ñ∏ add_one_pow_unbounded_of_pos _ (sub_pos.2 hy1)
#align pow_unbounded_of_one_lt pow_unbounded_of_one_lt

theorem exists_int_gt (x : Œ±) : ‚àÉ n : ‚Ñ§, x < n :=
  let ‚ü®n, h‚ü© := exists_nat_gt x
  ‚ü®n, by rwa [Int.cast_ofNat]‚ü©
         -- üéâ no goals
#align exists_int_gt exists_int_gt

theorem exists_int_lt (x : Œ±) : ‚àÉ n : ‚Ñ§, (n : Œ±) < x :=
  let ‚ü®n, h‚ü© := exists_int_gt (-x)
  ‚ü®-n, by rw [Int.cast_neg]; exact neg_lt.1 h‚ü©
          -- ‚ä¢ -‚Üën < x
                             -- üéâ no goals
#align exists_int_lt exists_int_lt

theorem exists_floor (x : Œ±) : ‚àÉ fl : ‚Ñ§, ‚àÄ z : ‚Ñ§, z ‚â§ fl ‚Üî (z : Œ±) ‚â§ x := by
  haveI := Classical.propDecidable
  -- ‚ä¢ ‚àÉ fl, ‚àÄ (z : ‚Ñ§), z ‚â§ fl ‚Üî ‚Üëz ‚â§ x
  have : ‚àÉ ub : ‚Ñ§, (ub : Œ±) ‚â§ x ‚àß ‚àÄ z : ‚Ñ§, (z : Œ±) ‚â§ x ‚Üí z ‚â§ ub :=
    Int.exists_greatest_of_bdd
      (let ‚ü®n, hn‚ü© := exists_int_gt x
      ‚ü®n, fun z h' => Int.cast_le.1 <| le_trans h' <| le_of_lt hn‚ü©)
      (let ‚ü®n, hn‚ü© := exists_int_lt x
      ‚ü®n, le_of_lt hn‚ü©)
  refine' this.imp fun fl h z => _
  -- ‚ä¢ z ‚â§ fl ‚Üî ‚Üëz ‚â§ x
  cases' h with h‚ÇÅ h‚ÇÇ
  -- ‚ä¢ z ‚â§ fl ‚Üî ‚Üëz ‚â§ x
  exact ‚ü®fun h => le_trans (Int.cast_le.2 h) h‚ÇÅ, h‚ÇÇ z‚ü©
  -- üéâ no goals
#align exists_floor exists_floor

end StrictOrderedRing

section LinearOrderedRing

variable [LinearOrderedRing Œ±] [Archimedean Œ±]

/-- Every x greater than or equal to 1 is between two successive
natural-number powers of every y greater than one. -/
theorem exists_nat_pow_near {x : Œ±} {y : Œ±} (hx : 1 ‚â§ x) (hy : 1 < y) :
    ‚àÉ n : ‚Ñï, y ^ n ‚â§ x ‚àß x < y ^ (n + 1) := by
  have h : ‚àÉ n : ‚Ñï, x < y ^ n := pow_unbounded_of_one_lt _ hy
  -- ‚ä¢ ‚àÉ n, y ^ n ‚â§ x ‚àß x < y ^ (n + 1)
  classical exact
      let n := Nat.find h
      have hn : x < y ^ n := Nat.find_spec h
      have hnp : 0 < n :=
        pos_iff_ne_zero.2 fun hn0 => by rw [hn0, pow_zero] at hn; exact not_le_of_gt hn hx
      have hnsp : Nat.pred n + 1 = n := Nat.succ_pred_eq_of_pos hnp
      have hltn : Nat.pred n < n := Nat.pred_lt (ne_of_gt hnp)
      ‚ü®Nat.pred n, le_of_not_lt (Nat.find_min h hltn), by rwa [hnsp]‚ü©
#align exists_nat_pow_near exists_nat_pow_near

end LinearOrderedRing

section LinearOrderedField

variable [LinearOrderedField Œ±] [Archimedean Œ±] {x y Œµ : Œ±}

/-- Every positive `x` is between two successive integer powers of
another `y` greater than one. This is the same as `exists_mem_Ioc_zpow`,
but with ‚â§ and < the other way around. -/
theorem exists_mem_Ico_zpow (hx : 0 < x) (hy : 1 < y) : ‚àÉ n : ‚Ñ§, x ‚àà Ico (y ^ n) (y ^ (n + 1)) := by
  classical exact
      let ‚ü®N, hN‚ü© := pow_unbounded_of_one_lt x‚Åª¬π hy
      have he : ‚àÉ m : ‚Ñ§, y ^ m ‚â§ x :=
        ‚ü®-N,
          le_of_lt
            (by
              rw [zpow_neg y ‚ÜëN, zpow_ofNat]
              exact (inv_lt hx (lt_trans (inv_pos.2 hx) hN)).1 hN)‚ü©
      let ‚ü®M, hM‚ü© := pow_unbounded_of_one_lt x hy
      have hb : ‚àÉ b : ‚Ñ§, ‚àÄ m, y ^ m ‚â§ x ‚Üí m ‚â§ b :=
        ‚ü®M, fun m hm =>
          le_of_not_lt fun hlt =>
            not_lt_of_ge (zpow_le_of_le hy.le hlt.le)
              (lt_of_le_of_lt hm (by rwa [‚Üê zpow_ofNat] at hM))‚ü©
      let ‚ü®n, hn‚ÇÅ, hn‚ÇÇ‚ü© := Int.exists_greatest_of_bdd hb he
      ‚ü®n, hn‚ÇÅ, lt_of_not_ge fun hge => not_le_of_gt (Int.lt_succ _) (hn‚ÇÇ _ hge)‚ü©
#align exists_mem_Ico_zpow exists_mem_Ico_zpow

/-- Every positive `x` is between two successive integer powers of
another `y` greater than one. This is the same as `exists_mem_Ico_zpow`,
but with ‚â§ and < the other way around. -/
theorem exists_mem_Ioc_zpow (hx : 0 < x) (hy : 1 < y) : ‚àÉ n : ‚Ñ§, x ‚àà Ioc (y ^ n) (y ^ (n + 1)) :=
  let ‚ü®m, hle, hlt‚ü© := exists_mem_Ico_zpow (inv_pos.2 hx) hy
  have hyp : 0 < y := lt_trans zero_lt_one hy
  ‚ü®-(m + 1), by rwa [zpow_neg, inv_lt (zpow_pos_of_pos hyp _) hx], by
                -- üéâ no goals
    rwa [neg_add, neg_add_cancel_right, zpow_neg, le_inv hx (zpow_pos_of_pos hyp _)]‚ü©
    -- üéâ no goals
#align exists_mem_Ioc_zpow exists_mem_Ioc_zpow

/-- For any `y < 1` and any positive `x`, there exists `n : ‚Ñï` with `y ^ n < x`. -/
theorem exists_pow_lt_of_lt_one (hx : 0 < x) (hy : y < 1) : ‚àÉ n : ‚Ñï, y ^ n < x := by
  by_cases y_pos : y ‚â§ 0
  -- ‚ä¢ ‚àÉ n, y ^ n < x
  ¬∑ use 1
    -- ‚ä¢ y ^ 1 < x
    simp only [pow_one]
    -- ‚ä¢ y < x
    linarith
    -- üéâ no goals
  rw [not_le] at y_pos
  -- ‚ä¢ ‚àÉ n, y ^ n < x
  rcases pow_unbounded_of_one_lt x‚Åª¬π (one_lt_inv y_pos hy) with ‚ü®q, hq‚ü©
  -- ‚ä¢ ‚àÉ n, y ^ n < x
  exact ‚ü®q, by rwa [inv_pow, inv_lt_inv hx (pow_pos y_pos _)] at hq‚ü©
  -- üéâ no goals
#align exists_pow_lt_of_lt_one exists_pow_lt_of_lt_one

/-- Given `x` and `y` between `0` and `1`, `x` is between two successive powers of `y`.
This is the same as `exists_nat_pow_near`, but for elements between `0` and `1` -/
theorem exists_nat_pow_near_of_lt_one (xpos : 0 < x) (hx : x ‚â§ 1) (ypos : 0 < y) (hy : y < 1) :
    ‚àÉ n : ‚Ñï, y ^ (n + 1) < x ‚àß x ‚â§ y ^ n := by
  rcases exists_nat_pow_near (one_le_inv_iff.2 ‚ü®xpos, hx‚ü©) (one_lt_inv_iff.2 ‚ü®ypos, hy‚ü©) with
    ‚ü®n, hn, h'n‚ü©
  refine' ‚ü®n, _, _‚ü©
  -- ‚ä¢ y ^ (n + 1) < x
  ¬∑ rwa [inv_pow, inv_lt_inv xpos (pow_pos ypos _)] at h'n
    -- üéâ no goals
  ¬∑ rwa [inv_pow, inv_le_inv (pow_pos ypos _) xpos] at hn
    -- üéâ no goals
#align exists_nat_pow_near_of_lt_one exists_nat_pow_near_of_lt_one

theorem exists_rat_gt (x : Œ±) : ‚àÉ q : ‚Ñö, x < q :=
  let ‚ü®n, h‚ü© := exists_nat_gt x
  ‚ü®n, by rwa [Rat.cast_coe_nat]‚ü©
         -- üéâ no goals
#align exists_rat_gt exists_rat_gt

theorem exists_rat_lt (x : Œ±) : ‚àÉ q : ‚Ñö, (q : Œ±) < x :=
  let ‚ü®n, h‚ü© := exists_int_lt x
  ‚ü®n, by rwa [Rat.cast_coe_int]‚ü©
         -- üéâ no goals
#align exists_rat_lt exists_rat_lt

theorem exists_rat_btwn {x y : Œ±} (h : x < y) : ‚àÉ q : ‚Ñö, x < q ‚àß (q : Œ±) < y := by
  cases' exists_nat_gt (y - x)‚Åª¬π with n nh
  -- ‚ä¢ ‚àÉ q, x < ‚Üëq ‚àß ‚Üëq < y
  cases' exists_floor (x * n) with z zh
  -- ‚ä¢ ‚àÉ q, x < ‚Üëq ‚àß ‚Üëq < y
  refine' ‚ü®(z + 1 : ‚Ñ§) / n, _‚ü©
  -- ‚ä¢ x < ‚Üë(‚Üë(z + 1) / ‚Üën) ‚àß ‚Üë(‚Üë(z + 1) / ‚Üën) < y
  have n0' := (inv_pos.2 (sub_pos.2 h)).trans nh
  -- ‚ä¢ x < ‚Üë(‚Üë(z + 1) / ‚Üën) ‚àß ‚Üë(‚Üë(z + 1) / ‚Üën) < y
  have n0 := Nat.cast_pos.1 n0'
  -- ‚ä¢ x < ‚Üë(‚Üë(z + 1) / ‚Üën) ‚àß ‚Üë(‚Üë(z + 1) / ‚Üën) < y
  rw [Rat.cast_div_of_ne_zero, Rat.cast_coe_nat, Rat.cast_coe_int, div_lt_iff n0']
  refine' ‚ü®(lt_div_iff n0').2 <| (lt_iff_lt_of_le_iff_le (zh _)).1 (lt_add_one _), _‚ü©
  rw [Int.cast_add, Int.cast_one]
  refine' lt_of_le_of_lt (add_le_add_right ((zh _).1 le_rfl) _) _
  rwa [‚Üê lt_sub_iff_add_lt', ‚Üê sub_mul, ‚Üê div_lt_iff' (sub_pos.2 h), one_div]
  ¬∑ rw [Rat.coe_int_den, Nat.cast_one]
    -- ‚ä¢ 1 ‚â† 0
    exact one_ne_zero
    -- üéâ no goals
  ¬∑ intro H
    -- ‚ä¢ False
    rw [Rat.coe_nat_num, Int.cast_ofNat, Nat.cast_eq_zero] at H
    -- ‚ä¢ False
    subst H
    -- ‚ä¢ False
    cases n0
    -- üéâ no goals
  ¬∑ rw [Rat.coe_nat_den, Nat.cast_one]
    -- ‚ä¢ 1 ‚â† 0
    exact one_ne_zero
    -- üéâ no goals
#align exists_rat_btwn exists_rat_btwn

theorem le_of_forall_rat_lt_imp_le (h : ‚àÄ q : ‚Ñö, (q : Œ±) < x ‚Üí (q : Œ±) ‚â§ y) : x ‚â§ y :=
  le_of_not_lt fun hyx =>
    let ‚ü®_, hy, hx‚ü© := exists_rat_btwn hyx
    hy.not_le <| h _ hx
#align le_of_forall_rat_lt_imp_le le_of_forall_rat_lt_imp_le

theorem le_of_forall_lt_rat_imp_le (h : ‚àÄ q : ‚Ñö, y < q ‚Üí x ‚â§ q) : x ‚â§ y :=
  le_of_not_lt fun hyx =>
    let ‚ü®_, hy, hx‚ü© := exists_rat_btwn hyx
    hx.not_le <| h _ hy
#align le_of_forall_lt_rat_imp_le le_of_forall_lt_rat_imp_le

theorem le_iff_forall_rat_lt_imp_le : x ‚â§ y ‚Üî ‚àÄ q : ‚Ñö, (q : Œ±) < x ‚Üí (q : Œ±) ‚â§ y :=
  ‚ü®fun hxy _ hqx ‚Ü¶ hqx.le.trans hxy, le_of_forall_rat_lt_imp_le‚ü©

theorem le_iff_forall_lt_rat_imp_le : x ‚â§ y ‚Üî ‚àÄ q : ‚Ñö, y < q ‚Üí x ‚â§ q :=
  ‚ü®fun hxy _ hqx ‚Ü¶ hxy.trans hqx.le, le_of_forall_lt_rat_imp_le‚ü©

theorem eq_of_forall_rat_lt_iff_lt (h : ‚àÄ q : ‚Ñö, (q : Œ±) < x ‚Üî (q : Œ±) < y) : x = y :=
  (le_of_forall_rat_lt_imp_le fun q hq => ((h q).1 hq).le).antisymm <|
    le_of_forall_rat_lt_imp_le fun q hq => ((h q).2 hq).le
#align eq_of_forall_rat_lt_iff_lt eq_of_forall_rat_lt_iff_lt

theorem eq_of_forall_lt_rat_iff_lt (h : ‚àÄ q : ‚Ñö, x < q ‚Üî y < q) : x = y :=
  (le_of_forall_lt_rat_imp_le fun q hq => ((h q).2 hq).le).antisymm <|
    le_of_forall_lt_rat_imp_le fun q hq => ((h q).1 hq).le
#align eq_of_forall_lt_rat_iff_lt eq_of_forall_lt_rat_iff_lt

theorem exists_nat_one_div_lt {Œµ : Œ±} (hŒµ : 0 < Œµ) : ‚àÉ n : ‚Ñï, 1 / (n + 1 : Œ±) < Œµ := by
  cases' exists_nat_gt (1 / Œµ) with n hn
  -- ‚ä¢ ‚àÉ n, 1 / (‚Üën + 1) < Œµ
  use n
  -- ‚ä¢ 1 / (‚Üën + 1) < Œµ
  rw [div_lt_iff, ‚Üê div_lt_iff' hŒµ]
  -- ‚ä¢ 1 / Œµ < ‚Üën + 1
  ¬∑ apply hn.trans
    -- ‚ä¢ ‚Üën < ‚Üën + 1
    simp [zero_lt_one]
    -- üéâ no goals
  ¬∑ exact n.cast_add_one_pos
    -- üéâ no goals
#align exists_nat_one_div_lt exists_nat_one_div_lt

theorem exists_pos_rat_lt {x : Œ±} (x0 : 0 < x) : ‚àÉ q : ‚Ñö, 0 < q ‚àß (q : Œ±) < x := by
  simpa only [Rat.cast_pos] using exists_rat_btwn x0
  -- üéâ no goals
#align exists_pos_rat_lt exists_pos_rat_lt

theorem exists_rat_near (x : Œ±) (Œµ0 : 0 < Œµ) : ‚àÉ q : ‚Ñö, |x - q| < Œµ :=
  let ‚ü®q, h‚ÇÅ, h‚ÇÇ‚ü© :=
    exists_rat_btwn <| ((sub_lt_self_iff x).2 Œµ0).trans ((lt_add_iff_pos_left x).2 Œµ0)
  ‚ü®q, abs_sub_lt_iff.2 ‚ü®sub_lt_comm.1 h‚ÇÅ, sub_lt_iff_lt_add.2 h‚ÇÇ‚ü©‚ü©
#align exists_rat_near exists_rat_near

end LinearOrderedField

section LinearOrderedField

variable [LinearOrderedField Œ±]

theorem archimedean_iff_nat_lt : Archimedean Œ± ‚Üî ‚àÄ x : Œ±, ‚àÉ n : ‚Ñï, x < n :=
  ‚ü®@exists_nat_gt Œ± _, fun H =>
    ‚ü®fun x y y0 =>
      (H (x / y)).imp fun n h => le_of_lt <| by rwa [div_lt_iff y0, ‚Üê nsmul_eq_mul] at h‚ü©‚ü©
                                                -- üéâ no goals
#align archimedean_iff_nat_lt archimedean_iff_nat_lt

theorem archimedean_iff_nat_le : Archimedean Œ± ‚Üî ‚àÄ x : Œ±, ‚àÉ n : ‚Ñï, x ‚â§ n :=
  archimedean_iff_nat_lt.trans
    ‚ü®fun H x => (H x).imp fun _ => le_of_lt, fun H x =>
      let ‚ü®n, h‚ü© := H x
      ‚ü®n + 1, lt_of_le_of_lt h (Nat.cast_lt.2 (lt_add_one _))‚ü©‚ü©
#align archimedean_iff_nat_le archimedean_iff_nat_le

theorem archimedean_iff_int_lt : Archimedean Œ± ‚Üî ‚àÄ x : Œ±, ‚àÉ n : ‚Ñ§, x < n :=
  ‚ü®@exists_int_gt Œ± _, by
    rw [archimedean_iff_nat_lt]
    -- ‚ä¢ (‚àÄ (x : Œ±), ‚àÉ n, x < ‚Üën) ‚Üí ‚àÄ (x : Œ±), ‚àÉ n, x < ‚Üën
    intro h x
    -- ‚ä¢ ‚àÉ n, x < ‚Üën
    obtain ‚ü®n, h‚ü© := h x
    -- ‚ä¢ ‚àÉ n, x < ‚Üën
    refine' ‚ü®n.toNat, h.trans_le _‚ü©
    -- ‚ä¢ ‚Üën ‚â§ ‚Üë(toNat n)
    exact_mod_cast Int.self_le_toNat _‚ü©
    -- üéâ no goals
#align archimedean_iff_int_lt archimedean_iff_int_lt

theorem archimedean_iff_int_le : Archimedean Œ± ‚Üî ‚àÄ x : Œ±, ‚àÉ n : ‚Ñ§, x ‚â§ n :=
  archimedean_iff_int_lt.trans
    ‚ü®fun H x => (H x).imp fun _ => le_of_lt, fun H x =>
      let ‚ü®n, h‚ü© := H x
      ‚ü®n + 1, lt_of_le_of_lt h (Int.cast_lt.2 (lt_add_one _))‚ü©‚ü©
#align archimedean_iff_int_le archimedean_iff_int_le

theorem archimedean_iff_rat_lt : Archimedean Œ± ‚Üî ‚àÄ x : Œ±, ‚àÉ q : ‚Ñö, x < q :=
  ‚ü®@exists_rat_gt Œ± _, fun H =>
    archimedean_iff_nat_lt.2 fun x =>
      let ‚ü®q, h‚ü© := H x
      ‚ü®‚åàq‚åâ‚Çä,
        lt_of_lt_of_le h <| by
          simpa only [Rat.cast_coe_nat] using (@Rat.cast_le Œ± _ _ _).2 (Nat.le_ceil _)‚ü©‚ü©
          -- üéâ no goals
#align archimedean_iff_rat_lt archimedean_iff_rat_lt

theorem archimedean_iff_rat_le : Archimedean Œ± ‚Üî ‚àÄ x : Œ±, ‚àÉ q : ‚Ñö, x ‚â§ q :=
  archimedean_iff_rat_lt.trans
    ‚ü®fun H x => (H x).imp fun _ => le_of_lt, fun H x =>
      let ‚ü®n, h‚ü© := H x
      ‚ü®n + 1, lt_of_le_of_lt h (Rat.cast_lt.2 (lt_add_one _))‚ü©‚ü©
#align archimedean_iff_rat_le archimedean_iff_rat_le

end LinearOrderedField

instance : Archimedean ‚Ñï :=
  ‚ü®fun n m m0 => ‚ü®n, by
    rw [‚Üê mul_one n, smul_eq_mul, mul_assoc, one_mul m]
    -- ‚ä¢ n * 1 ‚â§ n * m
    exact Nat.mul_le_mul_left n (by linarith)‚ü©‚ü©
    -- üéâ no goals

instance : Archimedean ‚Ñ§ :=
  ‚ü®fun n m m0 =>
    ‚ü®n.toNat,
      le_trans (Int.self_le_toNat _) <| by
        simpa only [nsmul_eq_mul, zero_add, mul_one] using
          mul_le_mul_of_nonneg_left (Int.add_one_le_iff.2 m0) (Int.ofNat_zero_le n.toNat)‚ü©‚ü©

instance : Archimedean ‚Ñö :=
  archimedean_iff_rat_le.2 fun q => ‚ü®q, by rw [Rat.cast_id]‚ü©
                                           -- üéâ no goals

/-- A linear ordered archimedean ring is a floor ring. This is not an `instance` because in some
cases we have a computable `floor` function. -/
noncomputable def Archimedean.floorRing (Œ±) [LinearOrderedRing Œ±] [Archimedean Œ±] : FloorRing Œ± :=
  FloorRing.ofFloor Œ± (fun a => Classical.choose (exists_floor a)) fun z a =>
    (Classical.choose_spec (exists_floor a) z).symm
#align archimedean.floor_ring Archimedean.floorRing

-- see Note [lower instance priority]
/-- A linear ordered field that is a floor ring is archimedean. -/
instance (priority := 100) FloorRing.archimedean (Œ±) [LinearOrderedField Œ±] [FloorRing Œ±] :
    Archimedean Œ± := by
  rw [archimedean_iff_int_le]
  -- ‚ä¢ ‚àÄ (x : Œ±), ‚àÉ n, x ‚â§ ‚Üën
  exact fun x => ‚ü®‚åàx‚åâ, Int.le_ceil x‚ü©
  -- üéâ no goals
#align floor_ring.archimedean FloorRing.archimedean
