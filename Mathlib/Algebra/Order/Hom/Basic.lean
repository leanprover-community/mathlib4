/-
Copyright (c) 2022 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies
-/

import Mathlib.Algebra.GroupPower.Order
import Mathlib.Tactic.Positivity.Basic

#align_import algebra.order.hom.basic from "leanprover-community/mathlib"@"28aa996fc6fb4317f0083c4e6daf79878d81be33"

/-!
# Algebraic order homomorphism classes

This file defines hom classes for common properties at the intersection of order theory and algebra.

## Typeclasses

Basic typeclasses
* `NonnegHomClass`: Homs are nonnegative: `‚àÄ f a, 0 ‚â§ f a`
* `SubadditiveHomClass`: Homs are subadditive: `‚àÄ f a b, f (a + b) ‚â§ f a + f b`
* `SubmultiplicativeHomClass`: Homs are submultiplicative: `‚àÄ f a b, f (a * b) ‚â§ f a * f b`
* `MulLEAddHomClass`: `‚àÄ f a b, f (a * b) ‚â§ f a + f b`
* `NonarchimedeanHomClass`: `‚àÄ a b, f (a + b) ‚â§ max (f a) (f b)`

Group norms
* `AddGroupSeminormClass`: Homs are nonnegative, subadditive, even and preserve zero.
* `GroupSeminormClass`: Homs are nonnegative, respect `f (a * b) ‚â§ f a + f b`, `f a‚Åª¬π = f a` and
  preserve zero.
* `AddGroupNormClass`: Homs are seminorms such that `f x = 0 ‚Üí x = 0` for all `x`.
* `GroupNormClass`: Homs are seminorms such that `f x = 0 ‚Üí x = 1` for all `x`.

Ring norms
* `RingSeminormClass`: Homs are submultiplicative group norms.
* `RingNormClass`: Homs are ring seminorms that are also additive group norms.
* `MulRingSeminormClass`: Homs are ring seminorms that are multiplicative.
* `MulRingNormClass`: Homs are ring norms that are multiplicative.

## Notes

Typeclasses for seminorms are defined here while types of seminorms are defined in
`Analysis.Normed.Group.Seminorm` and `Analysis.Normed.Ring.Seminorm` because absolute values are
multiplicative ring norms but outside of this use we only consider real-valued seminorms.

## TODO

Finitary versions of the current lemmas.
-/


library_note "out-param inheritance"/--
Diamond inheritance cannot depend on `outParam`s in the following circumstances:
 * there are three classes `Top`, `Middle`, `Bottom`
 * all of these classes have a parameter `(Œ± : outParam _)`
 * all of these classes have an instance parameter `[Root Œ±]` that depends on this `outParam`
 * the `Root` class has two child classes: `Left` and `Right`, these are siblings in the hierarchy
 * the instance `Bottom.toMiddle` takes a `[Left Œ±]` parameter
 * the instance `Middle.toTop` takes a `[Right Œ±]` parameter
 * there is a `Leaf` class that inherits from both `Left` and `Right`.
In that case, given instances `Bottom Œ±` and `Leaf Œ±`, Lean cannot synthesize a `Top Œ±` instance,
even though the hypotheses of the instances `Bottom.toMiddle` and `Middle.toTop` are satisfied.

There are two workarounds:
* You could replace the bundled inheritance implemented by the instance `Middle.toTop` with
  unbundled inheritance implemented by adding a `[Top Œ±]` parameter to the `Middle` class. This is
  the preferred option since it is also more compatible with Lean 4, at the cost of being more work
  to implement and more verbose to use.
* You could weaken the `Bottom.toMiddle` instance by making it depend on a subclass of
  `Middle.toTop`'s parameter, in this example replacing `[Left Œ±]` with `[Leaf Œ±]`.
-/

open Function

variable {Œπ F Œ± Œ≤ Œ≥ Œ¥ : Type*}

/-! ### Basics -/

/-- `NonnegHomClass F Œ± Œ≤` states that `F` is a type of nonnegative morphisms. -/
class NonnegHomClass (F : Type*) (Œ± Œ≤ : outParam (Type*)) [Zero Œ≤] [LE Œ≤] extends
  FunLike F Œ± fun _ => Œ≤ where
  /-- the image of any element is non negative. -/
  map_nonneg (f : F) : ‚àÄ a, 0 ‚â§ f a
#align nonneg_hom_class NonnegHomClass

/-- `SubadditiveHomClass F Œ± Œ≤` states that `F` is a type of subadditive morphisms. -/
class SubadditiveHomClass (F : Type*) (Œ± Œ≤ : outParam (Type*)) [Add Œ±] [Add Œ≤] [LE Œ≤] extends
  FunLike F Œ± fun _ => Œ≤ where
  /-- the image of a sum is less or equal than the sum of the images. -/
  map_add_le_add (f : F) : ‚àÄ a b, f (a + b) ‚â§ f a + f b
#align subadditive_hom_class SubadditiveHomClass

/-- `SubmultiplicativeHomClass F Œ± Œ≤` states that `F` is a type of submultiplicative morphisms. -/
@[to_additive SubadditiveHomClass]
class SubmultiplicativeHomClass (F : Type*) (Œ± Œ≤ : outParam (Type*)) [Mul Œ±] [Mul Œ≤] [LE Œ≤]
  extends FunLike F Œ± fun _ => Œ≤ where
  /-- the image of a product is less or equal than the product of the images. -/
  map_mul_le_mul (f : F) : ‚àÄ a b, f (a * b) ‚â§ f a * f b
#align submultiplicative_hom_class SubmultiplicativeHomClass

/-- `MulLEAddHomClass F Œ± Œ≤` states that `F` is a type of subadditive morphisms. -/
@[to_additive SubadditiveHomClass]
class MulLEAddHomClass (F : Type*) (Œ± Œ≤ : outParam (Type*)) [Mul Œ±] [Add Œ≤] [LE Œ≤]
  extends FunLike F Œ± fun _ => Œ≤ where
  /-- the image of a product is less or equal than the sum of the images. -/
  map_mul_le_add (f : F) : ‚àÄ a b, f (a * b) ‚â§ f a + f b
#align mul_le_add_hom_class MulLEAddHomClass

/-- `NonarchimedeanHomClass F Œ± Œ≤` states that `F` is a type of non-archimedean morphisms. -/
class NonarchimedeanHomClass (F : Type*) (Œ± Œ≤ : outParam (Type*)) [Add Œ±] [LinearOrder Œ≤]
  extends FunLike F Œ± fun _ => Œ≤ where
  /-- the image of a sum is less or equal than the maximum of the images. -/
  map_add_le_max (f : F) : ‚àÄ a b, f (a + b) ‚â§ max (f a) (f b)
#align nonarchimedean_hom_class NonarchimedeanHomClass

export NonnegHomClass (map_nonneg)

export SubadditiveHomClass (map_add_le_add)

export SubmultiplicativeHomClass (map_mul_le_mul)

export MulLEAddHomClass (map_mul_le_add)

export NonarchimedeanHomClass (map_add_le_max)

attribute [simp] map_nonneg

@[to_additive]
theorem le_map_mul_map_div [Group Œ±] [CommSemigroup Œ≤] [LE Œ≤] [SubmultiplicativeHomClass F Œ± Œ≤]
    (f : F) (a b : Œ±) : f a ‚â§ f b * f (a / b) := by
  simpa only [mul_comm, div_mul_cancel'] using map_mul_le_mul f (a / b) b
  -- üéâ no goals
#align le_map_mul_map_div le_map_mul_map_div
#align le_map_add_map_sub le_map_add_map_sub

@[to_additive existing]
theorem le_map_add_map_div [Group Œ±] [AddCommSemigroup Œ≤] [LE Œ≤] [MulLEAddHomClass F Œ± Œ≤] (f : F)
    (a b : Œ±) : f a ‚â§ f b + f (a / b) := by
  simpa only [add_comm, div_mul_cancel'] using map_mul_le_add f (a / b) b
  -- üéâ no goals
#align le_map_add_map_div le_map_add_map_div
-- #align le_map_add_map_sub le_map_add_map_sub -- Porting note: TODO: `to_additive` clashes

@[to_additive]
theorem le_map_div_mul_map_div [Group Œ±] [CommSemigroup Œ≤] [LE Œ≤] [SubmultiplicativeHomClass F Œ± Œ≤]
    (f : F) (a b c : Œ±) : f (a / c) ‚â§ f (a / b) * f (b / c) := by
  simpa only [div_mul_div_cancel'] using map_mul_le_mul f (a / b) (b / c)
  -- üéâ no goals
#align le_map_div_mul_map_div le_map_div_mul_map_div
#align le_map_sub_add_map_sub le_map_sub_add_map_sub

@[to_additive existing]
theorem le_map_div_add_map_div [Group Œ±] [AddCommSemigroup Œ≤] [LE Œ≤] [MulLEAddHomClass F Œ± Œ≤]
    (f : F) (a b c : Œ±) : f (a / c) ‚â§ f (a / b) + f (b / c) := by
    simpa only [div_mul_div_cancel'] using map_mul_le_add f (a / b) (b / c)
    -- üéâ no goals
#align le_map_div_add_map_div le_map_div_add_map_div
-- #align le_map_sub_add_map_sub le_map_sub_add_map_sub -- Porting note: TODO: `to_additive` clashes

namespace Mathlib.Meta.Positivity

open Lean Meta Qq Function

/-- Extension for the `positivity` tactic: nonnegative maps take nonnegative values. -/
@[positivity FunLike.coe _ _]
def evalMap : PositivityExt where eval {_ Œ≤} _ _ e := do
  let .app (.app _ f) a ‚Üê whnfR e
    | throwError "not ‚Üëf ¬∑ where f is of NonnegHomClass"
  let pa ‚Üê mkAppOptM ``map_nonneg #[none, none, Œ≤, none, none, none, f, a]
  pure (.nonnegative pa)

end Mathlib.Meta.Positivity

/-! ### Group (semi)norms -/


/-- `AddGroupSeminormClass F Œ±` states that `F` is a type of `Œ≤`-valued seminorms on the additive
group `Œ±`.

You should extend this class when you extend `AddGroupSeminorm`. -/
class AddGroupSeminormClass (F : Type*) (Œ± Œ≤ : outParam <| Type*) [AddGroup Œ±]
  [OrderedAddCommMonoid Œ≤] extends SubadditiveHomClass F Œ± Œ≤ where
  /-- The image of zero is zero. -/
  map_zero (f : F) : f 0 = 0
  /-- The map is invariant under negation of its argument. -/
  map_neg_eq_map (f : F) (a : Œ±) : f (-a) = f a
#align add_group_seminorm_class AddGroupSeminormClass

/-- `GroupSeminormClass F Œ±` states that `F` is a type of `Œ≤`-valued seminorms on the group `Œ±`.

You should extend this class when you extend `GroupSeminorm`. -/
@[to_additive]
class GroupSeminormClass (F : Type*) (Œ± Œ≤ : outParam <| Type*) [Group Œ±]
  [OrderedAddCommMonoid Œ≤] extends MulLEAddHomClass F Œ± Œ≤ where
  /-- The image of one is zero. -/
  map_one_eq_zero (f : F) : f 1 = 0
  /-- The map is invariant under inversion of its argument. -/
  map_inv_eq_map (f : F) (a : Œ±) : f a‚Åª¬π = f a
#align group_seminorm_class GroupSeminormClass

/-- `AddGroupNormClass F Œ±` states that `F` is a type of `Œ≤`-valued norms on the additive group
`Œ±`.

You should extend this class when you extend `AddGroupNorm`. -/
class AddGroupNormClass (F : Type*) (Œ± Œ≤ : outParam <| Type*) [AddGroup Œ±]
  [OrderedAddCommMonoid Œ≤] extends AddGroupSeminormClass F Œ± Œ≤ where
  /-- The argument is zero if its image under the map is zero. -/
  eq_zero_of_map_eq_zero (f : F) {a : Œ±} : f a = 0 ‚Üí a = 0
#align add_group_norm_class AddGroupNormClass

/-- `GroupNormClass F Œ±` states that `F` is a type of `Œ≤`-valued norms on the group `Œ±`.

You should extend this class when you extend `GroupNorm`. -/
@[to_additive]
class GroupNormClass (F : Type*) (Œ± Œ≤ : outParam <| Type*) [Group Œ±]
  [OrderedAddCommMonoid Œ≤] extends GroupSeminormClass F Œ± Œ≤ where
  /-- The argument is one if its image under the map is zero. -/
  eq_one_of_map_eq_zero (f : F) {a : Œ±} : f a = 0 ‚Üí a = 1
#align group_norm_class GroupNormClass

export AddGroupSeminormClass (map_neg_eq_map)

export GroupSeminormClass (map_one_eq_zero map_inv_eq_map)

export AddGroupNormClass (eq_zero_of_map_eq_zero)

export GroupNormClass (eq_one_of_map_eq_zero)

attribute [simp] map_one_eq_zero -- porting note: `to_additive` translation already exists

attribute [simp] map_neg_eq_map

attribute [simp] map_inv_eq_map -- porting note: `to_additive` translation already exists

attribute [to_additive] GroupSeminormClass.toMulLEAddHomClass

-- See note [lower instance priority]
instance (priority := 100) AddGroupSeminormClass.toZeroHomClass [AddGroup Œ±]
    [OrderedAddCommMonoid Œ≤] [AddGroupSeminormClass F Œ± Œ≤] : ZeroHomClass F Œ± Œ≤ :=
  { ‚ÄπAddGroupSeminormClass F Œ± Œ≤‚Ä∫ with }
#align add_group_seminorm_class.to_zero_hom_class AddGroupSeminormClass.toZeroHomClass

section GroupSeminormClass

variable [Group Œ±] [OrderedAddCommMonoid Œ≤] [GroupSeminormClass F Œ± Œ≤] (f : F) (x y : Œ±)

@[to_additive]
theorem map_div_le_add : f (x / y) ‚â§ f x + f y := by
  rw [div_eq_mul_inv, ‚Üê map_inv_eq_map f y]
  -- ‚ä¢ ‚Üëf (x * y‚Åª¬π) ‚â§ ‚Üëf x + ‚Üëf y‚Åª¬π
  exact map_mul_le_add _ _ _
  -- üéâ no goals
#align map_div_le_add map_div_le_add
#align map_sub_le_add map_sub_le_add

@[to_additive]
theorem map_div_rev : f (x / y) = f (y / x) := by rw [‚Üê inv_div, map_inv_eq_map]
                                                  -- üéâ no goals
#align map_div_rev map_div_rev
#align map_sub_rev map_sub_rev

@[to_additive]
theorem le_map_add_map_div' : f x ‚â§ f y + f (y / x) := by
  simpa only [add_comm, map_div_rev, div_mul_cancel'] using map_mul_le_add f (x / y) y
  -- üéâ no goals
#align le_map_add_map_div' le_map_add_map_div'
#align le_map_add_map_sub' le_map_add_map_sub'

end GroupSeminormClass

example [OrderedAddCommGroup Œ≤] : OrderedAddCommMonoid Œ≤ :=
  inferInstance

@[to_additive]
theorem abs_sub_map_le_div [Group Œ±] [LinearOrderedAddCommGroup Œ≤] [GroupSeminormClass F Œ± Œ≤]
    (f : F) (x y : Œ±) : |f x - f y| ‚â§ f (x / y) := by
  rw [abs_sub_le_iff, sub_le_iff_le_add', sub_le_iff_le_add']
  -- ‚ä¢ ‚Üëf x ‚â§ ‚Üëf y + ‚Üëf (x / y) ‚àß ‚Üëf y ‚â§ ‚Üëf x + ‚Üëf (x / y)
  exact ‚ü®le_map_add_map_div _ _ _, le_map_add_map_div' _ _ _‚ü©
  -- üéâ no goals
#align abs_sub_map_le_div abs_sub_map_le_div
#align abs_sub_map_le_sub abs_sub_map_le_sub

-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) GroupSeminormClass.toNonnegHomClass [Group Œ±]
    [LinearOrderedAddCommMonoid Œ≤] [GroupSeminormClass F Œ± Œ≤] : NonnegHomClass F Œ± Œ≤ :=
  { ‚ÄπGroupSeminormClass F Œ± Œ≤‚Ä∫ with
    map_nonneg := fun f a =>
      (nsmul_nonneg_iff two_ne_zero).1 <| by
        rw [two_nsmul, ‚Üê map_one_eq_zero f, ‚Üê div_self' a]
        -- ‚ä¢ ‚Üëf (a / a) ‚â§ ‚Üëf a + ‚Üëf a
        exact map_div_le_add _ _ _ }
        -- üéâ no goals
#align group_seminorm_class.to_nonneg_hom_class GroupSeminormClass.toNonnegHomClass
#align add_group_seminorm_class.to_nonneg_hom_class AddGroupSeminormClass.toNonnegHomClass

section GroupNormClass

variable [Group Œ±] [OrderedAddCommMonoid Œ≤] [GroupNormClass F Œ± Œ≤] (f : F) {x : Œ±}

@[to_additive (attr := simp)]
theorem map_eq_zero_iff_eq_one : f x = 0 ‚Üî x = 1 :=
  ‚ü®eq_one_of_map_eq_zero _, by
    rintro rfl
    -- ‚ä¢ ‚Üëf 1 = 0
    exact map_one_eq_zero _‚ü©
    -- üéâ no goals
#align map_eq_zero_iff_eq_one map_eq_zero_iff_eq_one
#align map_eq_zero_iff_eq_zero map_eq_zero_iff_eq_zero

@[to_additive]
theorem map_ne_zero_iff_ne_one : f x ‚â† 0 ‚Üî x ‚â† 1 :=
  (map_eq_zero_iff_eq_one _).not
#align map_ne_zero_iff_ne_one map_ne_zero_iff_ne_one
#align map_ne_zero_iff_ne_zero map_ne_zero_iff_ne_zero

end GroupNormClass

@[to_additive]
theorem map_pos_of_ne_one [Group Œ±] [LinearOrderedAddCommMonoid Œ≤] [GroupNormClass F Œ± Œ≤] (f : F)
    {x : Œ±} (hx : x ‚â† 1) : 0 < f x :=
  (map_nonneg _ _).lt_of_ne <| ((map_ne_zero_iff_ne_one _).2 hx).symm
#align map_pos_of_ne_one map_pos_of_ne_one
#align map_pos_of_ne_zero map_pos_of_ne_zero

/-! ### Ring (semi)norms -/


/-- `RingSeminormClass F Œ±` states that `F` is a type of `Œ≤`-valued seminorms on the ring `Œ±`.

You should extend this class when you extend `RingSeminorm`. -/
class RingSeminormClass (F : Type*) (Œ± Œ≤ : outParam <| Type*) [NonUnitalNonAssocRing Œ±]
  [OrderedSemiring Œ≤] extends AddGroupSeminormClass F Œ± Œ≤, SubmultiplicativeHomClass F Œ± Œ≤
#align ring_seminorm_class RingSeminormClass

/-- `RingNormClass F Œ±` states that `F` is a type of `Œ≤`-valued norms on the ring `Œ±`.

You should extend this class when you extend `RingNorm`. -/
class RingNormClass (F : Type*) (Œ± Œ≤ : outParam <| Type*) [NonUnitalNonAssocRing Œ±]
  [OrderedSemiring Œ≤] extends RingSeminormClass F Œ± Œ≤, AddGroupNormClass F Œ± Œ≤
#align ring_norm_class RingNormClass

/-- `MulRingSeminormClass F Œ±` states that `F` is a type of `Œ≤`-valued multiplicative seminorms
on the ring `Œ±`.

You should extend this class when you extend `MulRingSeminorm`. -/
class MulRingSeminormClass (F : Type*) (Œ± Œ≤ : outParam <| Type*) [NonAssocRing Œ±]
  [OrderedSemiring Œ≤] extends AddGroupSeminormClass F Œ± Œ≤, MonoidWithZeroHomClass F Œ± Œ≤
#align mul_ring_seminorm_class MulRingSeminormClass

/-- `MulRingNormClass F Œ±` states that `F` is a type of `Œ≤`-valued multiplicative norms on the
ring `Œ±`.

You should extend this class when you extend `MulRingNorm`. -/
class MulRingNormClass (F : Type*) (Œ± Œ≤ : outParam <| Type*) [NonAssocRing Œ±]
  [OrderedSemiring Œ≤] extends MulRingSeminormClass F Œ± Œ≤, AddGroupNormClass F Œ± Œ≤
#align mul_ring_norm_class MulRingNormClass

-- See note [out-param inheritance]
-- See note [lower instance priority]
instance (priority := 100) RingSeminormClass.toNonnegHomClass [NonUnitalNonAssocRing Œ±]
    [LinearOrderedSemiring Œ≤] [RingSeminormClass F Œ± Œ≤] : NonnegHomClass F Œ± Œ≤ :=
  AddGroupSeminormClass.toNonnegHomClass
#align ring_seminorm_class.to_nonneg_hom_class RingSeminormClass.toNonnegHomClass

-- See note [lower instance priority]
instance (priority := 100) MulRingSeminormClass.toRingSeminormClass [NonAssocRing Œ±]
    [OrderedSemiring Œ≤] [MulRingSeminormClass F Œ± Œ≤] : RingSeminormClass F Œ± Œ≤ :=
  { ‚ÄπMulRingSeminormClass F Œ± Œ≤‚Ä∫ with map_mul_le_mul := fun f a b => (map_mul _ _ _).le }
#align mul_ring_seminorm_class.to_ring_seminorm_class MulRingSeminormClass.toRingSeminormClass

-- See note [lower instance priority]
instance (priority := 100) MulRingNormClass.toRingNormClass [NonAssocRing Œ±]
    [OrderedSemiring Œ≤] [MulRingNormClass F Œ± Œ≤] : RingNormClass F Œ± Œ≤ :=
  { ‚ÄπMulRingNormClass F Œ± Œ≤‚Ä∫, MulRingSeminormClass.toRingSeminormClass with }
#align mul_ring_norm_class.to_ring_norm_class MulRingNormClass.toRingNormClass
