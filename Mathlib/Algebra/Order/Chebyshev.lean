/-
Copyright (c) 2023 Mantas BakÅ¡ys, YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mantas BakÅ¡ys, YaÃ«l Dillies
-/
import Mathlib.Algebra.BigOperators.Order
import Mathlib.Algebra.Order.Rearrangement
import Mathlib.GroupTheory.Perm.Cycle.Basic

#align_import algebra.order.chebyshev from "leanprover-community/mathlib"@"b7399344324326918d65d0c74e9571e3a8cb9199"

/-!
# Chebyshev's sum inequality

This file proves the Chebyshev sum inequality.

Chebyshev's inequality states `(âˆ‘ i in s, f i) * (âˆ‘ i in s, g i) â‰¤ s.card * âˆ‘ i in s, f i * g i`
when `f g : Î¹ â†’ Î±` monovary, and the reverse inequality when `f` and `g` antivary.


## Main declarations

* `MonovaryOn.sum_mul_sum_le_card_mul_sum`: Chebyshev's inequality.
* `AntivaryOn.card_mul_sum_le_sum_mul_sum`: Chebyshev's inequality, dual version.
* `sq_sum_le_card_mul_sum_sq`: Special case of Chebyshev's inequality when `f = g`.

## Implementation notes

In fact, we don't need much compatibility between the addition and multiplication of `Î±`, so we can
actually decouple them by replacing multiplication with scalar multiplication and making `f` and `g`
land in different types.
As a bonus, this makes the dual statement trivial. The multiplication versions are provided for
convenience.

The case for `Monotone`/`Antitone` pairs of functions over a `LinearOrder` is not deduced in this
file because it is easily deducible from the `Monovary` API.
-/


open Equiv Equiv.Perm Finset Function OrderDual

open BigOperators

variable {Î¹ Î± Î² : Type*}

/-! ### Scalar multiplication versions -/


section Smul

variable [LinearOrderedRing Î±] [LinearOrderedAddCommGroup Î²] [Module Î± Î²] [OrderedSMul Î± Î²]
  {s : Finset Î¹} {Ïƒ : Perm Î¹} {f : Î¹ â†’ Î±} {g : Î¹ â†’ Î²}

/-- **Chebyshev's Sum Inequality**: When `f` and `g` monovary together (eg they are both
monotone/antitone), the scalar product of their sum is less than the size of the set times their
scalar product. -/
theorem MonovaryOn.sum_smul_sum_le_card_smul_sum (hfg : MonovaryOn f g s) :
    ((âˆ‘ i in s, f i) â€¢ âˆ‘ i in s, g i) â‰¤ s.card â€¢ âˆ‘ i in s, f i â€¢ g i := by
  classical
    obtain âŸ¨Ïƒ, hÏƒ, hsâŸ© := s.countable_toSet.exists_cycleOn
    rw [â† card_range s.card, sum_smul_sum_eq_sum_perm hÏƒ]
    exact
      sum_le_card_nsmul _ _ _ fun n _ =>
        hfg.sum_smul_comp_perm_le_sum_smul fun x hx => hs fun h => hx <| IsFixedPt.perm_pow h _
#align monovary_on.sum_smul_sum_le_card_smul_sum MonovaryOn.sum_smul_sum_le_card_smul_sum

/-- **Chebyshev's Sum Inequality**: When `f` and `g` antivary together (eg one is monotone, the
other is antitone), the scalar product of their sum is less than the size of the set times their
scalar product. -/
theorem AntivaryOn.card_smul_sum_le_sum_smul_sum (hfg : AntivaryOn f g s) :
    (s.card â€¢ âˆ‘ i in s, f i â€¢ g i) â‰¤ (âˆ‘ i in s, f i) â€¢ âˆ‘ i in s, g i := by
  refine hfg.dual_right.sum_smul_sum_le_card_smul_sum
  -- ðŸŽ‰ no goals
#align antivary_on.card_smul_sum_le_sum_smul_sum AntivaryOn.card_smul_sum_le_sum_smul_sum

variable [Fintype Î¹]

/-- **Chebyshev's Sum Inequality**: When `f` and `g` monovary together (eg they are both
monotone/antitone), the scalar product of their sum is less than the size of the set times their
scalar product. -/
theorem Monovary.sum_smul_sum_le_card_smul_sum (hfg : Monovary f g) :
    ((âˆ‘ i, f i) â€¢ âˆ‘ i, g i) â‰¤ Fintype.card Î¹ â€¢ âˆ‘ i, f i â€¢ g i :=
  (hfg.monovaryOn _).sum_smul_sum_le_card_smul_sum
#align monovary.sum_smul_sum_le_card_smul_sum Monovary.sum_smul_sum_le_card_smul_sum

/-- **Chebyshev's Sum Inequality**: When `f` and `g` antivary together (eg one is monotone, the
other is antitone), the scalar product of their sum is less than the size of the set times their
scalar product. -/
theorem Antivary.card_smul_sum_le_sum_smul_sum (hfg : Antivary f g) :
    (Fintype.card Î¹ â€¢ âˆ‘ i, f i â€¢ g i) â‰¤ (âˆ‘ i, f i) â€¢ âˆ‘ i, g i := by
  refine (hfg.dual_right.monovaryOn _).sum_smul_sum_le_card_smul_sum
  -- ðŸŽ‰ no goals
#align antivary.card_smul_sum_le_sum_smul_sum Antivary.card_smul_sum_le_sum_smul_sum

end Smul

/-!
### Multiplication versions

Special cases of the above when scalar multiplication is actually multiplication.
-/


section Mul

variable [LinearOrderedRing Î±] {s : Finset Î¹} {Ïƒ : Perm Î¹} {f g : Î¹ â†’ Î±}

/-- **Chebyshev's Sum Inequality**: When `f` and `g` monovary together (eg they are both
monotone/antitone), the product of their sum is less than the size of the set times their scalar
product. -/
theorem MonovaryOn.sum_mul_sum_le_card_mul_sum (hfg : MonovaryOn f g s) :
    ((âˆ‘ i in s, f i) * âˆ‘ i in s, g i) â‰¤ s.card * âˆ‘ i in s, f i * g i := by
  rw [â† nsmul_eq_mul]
  -- âŠ¢ (âˆ‘ i in s, f i) * âˆ‘ i in s, g i â‰¤ card s â€¢ âˆ‘ i in s, f i * g i
  exact hfg.sum_smul_sum_le_card_smul_sum
  -- ðŸŽ‰ no goals
#align monovary_on.sum_mul_sum_le_card_mul_sum MonovaryOn.sum_mul_sum_le_card_mul_sum

/-- **Chebyshev's Sum Inequality**: When `f` and `g` antivary together (eg one is monotone, the
other is antitone), the product of their sum is greater than the size of the set times their scalar
product. -/
theorem AntivaryOn.card_mul_sum_le_sum_mul_sum (hfg : AntivaryOn f g s) :
    ((s.card : Î±) * âˆ‘ i in s, f i * g i) â‰¤ (âˆ‘ i in s, f i) * âˆ‘ i in s, g i := by
  rw [â† nsmul_eq_mul]
  -- âŠ¢ card s â€¢ âˆ‘ i in s, f i * g i â‰¤ (âˆ‘ i in s, f i) * âˆ‘ i in s, g i
  exact hfg.card_smul_sum_le_sum_smul_sum
  -- ðŸŽ‰ no goals
#align antivary_on.card_mul_sum_le_sum_mul_sum AntivaryOn.card_mul_sum_le_sum_mul_sum

/-- Special case of **Chebyshev's Sum Inequality** or the **Cauchy-Schwarz Inequality**: The square
of the sum is less than the size of the set times the sum of the squares. -/
theorem sq_sum_le_card_mul_sum_sq : (âˆ‘ i in s, f i) ^ 2 â‰¤ s.card * âˆ‘ i in s, f i ^ 2 := by
  simp_rw [sq]
  -- âŠ¢ (âˆ‘ i in s, f i) * âˆ‘ i in s, f i â‰¤ â†‘(card s) * âˆ‘ x in s, f x * f x
  exact (monovaryOn_self _ _).sum_mul_sum_le_card_mul_sum
  -- ðŸŽ‰ no goals
#align sq_sum_le_card_mul_sum_sq sq_sum_le_card_mul_sum_sq

variable [Fintype Î¹]

/-- **Chebyshev's Sum Inequality**: When `f` and `g` monovary together (eg they are both
monotone/antitone), the product of their sum is less than the size of the set times their scalar
product. -/
theorem Monovary.sum_mul_sum_le_card_mul_sum (hfg : Monovary f g) :
    ((âˆ‘ i, f i) * âˆ‘ i, g i) â‰¤ Fintype.card Î¹ * âˆ‘ i, f i * g i :=
  (hfg.monovaryOn _).sum_mul_sum_le_card_mul_sum
#align monovary.sum_mul_sum_le_card_mul_sum Monovary.sum_mul_sum_le_card_mul_sum

/-- **Chebyshev's Sum Inequality**: When `f` and `g` antivary together (eg one is monotone, the
other is antitone), the product of their sum is less than the size of the set times their scalar
product. -/
theorem Antivary.card_mul_sum_le_sum_mul_sum (hfg : Antivary f g) :
    ((Fintype.card Î¹ : Î±) * âˆ‘ i, f i * g i) â‰¤ (âˆ‘ i, f i) * âˆ‘ i, g i :=
  (hfg.antivaryOn _).card_mul_sum_le_sum_mul_sum
#align antivary.card_mul_sum_le_sum_mul_sum Antivary.card_mul_sum_le_sum_mul_sum

end Mul

variable [LinearOrderedField Î±] {s : Finset Î¹} {f : Î¹ â†’ Î±}

theorem sum_div_card_sq_le_sum_sq_div_card :
    ((âˆ‘ i in s, f i) / s.card) ^ 2 â‰¤ (âˆ‘ i in s, f i ^ 2) / s.card := by
  obtain rfl | hs := s.eq_empty_or_nonempty
  -- âŠ¢ ((âˆ‘ i in âˆ…, f i) / â†‘(card âˆ…)) ^ 2 â‰¤ (âˆ‘ i in âˆ…, f i ^ 2) / â†‘(card âˆ…)
  Â· simp
    -- ðŸŽ‰ no goals
  rw [â† card_pos, â† @Nat.cast_pos Î±] at hs
  -- âŠ¢ ((âˆ‘ i in s, f i) / â†‘(card s)) ^ 2 â‰¤ (âˆ‘ i in s, f i ^ 2) / â†‘(card s)
  rw [div_pow, div_le_div_iff (sq_pos_of_ne_zero _ hs.ne') hs, sq (s.card : Î±), mul_left_comm, â†
    mul_assoc]
  exact mul_le_mul_of_nonneg_right sq_sum_le_card_mul_sum_sq hs.le
  -- ðŸŽ‰ no goals
#align sum_div_card_sq_le_sum_sq_div_card sum_div_card_sq_le_sum_sq_div_card
