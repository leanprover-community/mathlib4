/-
Copyright (c) 2022 Siddhartha Prasad, YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Siddhartha Prasad, YaÃ«l Dillies
-/
import Mathlib.Algebra.Order.Ring.Canonical
import Mathlib.Algebra.Ring.Pi
import Mathlib.Algebra.Ring.Prod
import Mathlib.Order.Hom.CompleteLattice

#align_import algebra.order.kleene from "leanprover-community/mathlib"@"98e83c3d541c77cdb7da20d79611a780ff8e7d90"

/-!
# Kleene Algebras

This file defines idempotent semirings and Kleene algebras, which are used extensively in the theory
of computation.

An idempotent semiring is a semiring whose addition is idempotent. An idempotent semiring is
naturally a semilattice by setting `a â‰¤ b` if `a + b = b`.

A Kleene algebra is an idempotent semiring equipped with an additional unary operator `âˆ—`, the
Kleene star.

## Main declarations

* `IdemSemiring`: Idempotent semiring
* `IdemCommSemiring`: Idempotent commutative semiring
* `KleeneAlgebra`: Kleene algebra

## Notation

`aâˆ—` is notation for `kstar a` in locale `Computability`.

## References

* [D. Kozen, *A completeness theorem for Kleene algebras and the algebra of regular events*]
  [kozen1994]
* https://planetmath.org/idempotentsemiring
* https://encyclopediaofmath.org/wiki/Idempotent_semi-ring
* https://planetmath.org/kleene_algebra

## TODO

Instances for `AddOpposite`, `MulOpposite`, `ULift`, `Subsemiring`, `Subring`, `Subalgebra`.

## Tags

kleene algebra, idempotent semiring
-/


open Function

universe u

variable {Î± Î² Î¹ : Type*} {Ï€ : Î¹ â†’ Type*}

/-- An idempotent semiring is a semiring with the additional property that addition is idempotent.
-/
class IdemSemiring (Î± : Type u) extends Semiring Î±, SemilatticeSup Î± where
  protected sup := (Â· + Â·)
  protected add_eq_sup : âˆ€ a b : Î±, a + b = a âŠ” b := by
    intros
    rfl
  protected bot : Î± := 0
  protected bot_le : âˆ€ a, bot â‰¤ a
#align idem_semiring IdemSemiring

/-- An idempotent commutative semiring is a commutative semiring with the additional property that
addition is idempotent. -/
class IdemCommSemiring (Î± : Type u) extends CommSemiring Î±, IdemSemiring Î±
#align idem_comm_semiring IdemCommSemiring

/-- Notation typeclass for the Kleene star `âˆ—`. -/
class KStar (Î± : Type*) where
  protected kstar : Î± â†’ Î±
#align has_kstar KStar

-- mathport name: Â«expr âˆ—Â»
scoped[Computability] postfix:1024 "âˆ—" => KStar.kstar
open Computability

/-- A Kleene Algebra is an idempotent semiring with an additional unary operator `kstar` (for Kleene
star) that satisfies the following properties:
* `1 + a * aâˆ— â‰¤ aâˆ—`
* `1 + aâˆ— * a â‰¤ aâˆ—`
* If `a * c + b â‰¤ c`, then `aâˆ— * b â‰¤ c`
* If `c * a + b â‰¤ c`, then `b * aâˆ— â‰¤ c`
-/
class KleeneAlgebra (Î± : Type*) extends IdemSemiring Î±, KStar Î± where
  protected one_le_kstar : âˆ€ a : Î±, 1 â‰¤ aâˆ—
  protected mul_kstar_le_kstar : âˆ€ a : Î±, a * aâˆ— â‰¤ aâˆ—
  protected kstar_mul_le_kstar : âˆ€ a : Î±, aâˆ— * a â‰¤ aâˆ—
  protected mul_kstar_le_self : âˆ€ a b : Î±, b * a â‰¤ b â†’ b * aâˆ— â‰¤ b
  protected kstar_mul_le_self : âˆ€ a b : Î±, a * b â‰¤ b â†’ aâˆ— * b â‰¤ b
#align kleene_algebra KleeneAlgebra

-- See note [lower instance priority]
instance (priority := 100) IdemSemiring.toOrderBot [IdemSemiring Î±] : OrderBot Î± :=
  { â€¹IdemSemiring Î±â€º with }
#align idem_semiring.to_order_bot IdemSemiring.toOrderBot

-- See note [reducible non-instances]
/-- Construct an idempotent semiring from an idempotent addition. -/
@[reducible]
def IdemSemiring.ofSemiring [Semiring Î±] (h : âˆ€ a : Î±, a + a = a) : IdemSemiring Î± :=
  { â€¹Semiring Î±â€º with
    le := fun a b â†¦ a + b = b
    le_refl := h
    le_trans := fun a b c hab hbc â†¦ by
      simp only
      -- âŠ¢ a + c = c
      rw [â† hbc, â† add_assoc, hab]
      -- ðŸŽ‰ no goals
    le_antisymm := fun a b hab hba â†¦ by rwa [â† hba, add_comm]
                                        -- ðŸŽ‰ no goals
    sup := (Â· + Â·)
    le_sup_left := fun a b â†¦ by
      simp only
      -- âŠ¢ a + (a + b) = a + b
      rw [â† add_assoc, h]
      -- ðŸŽ‰ no goals
    le_sup_right := fun a b â†¦ by
      simp only
      -- âŠ¢ b + (a + b) = a + b
      rw [add_comm, add_assoc, h]
      -- ðŸŽ‰ no goals
    sup_le := fun a b c hab hbc â†¦ by
      simp only
      -- âŠ¢ a + b + c = c
      rwa [add_assoc, hbc]
      -- ðŸŽ‰ no goals
    bot := 0
    bot_le := zero_add }
#align idem_semiring.of_semiring IdemSemiring.ofSemiring

section IdemSemiring

variable [IdemSemiring Î±] {a b c : Î±}

theorem add_eq_sup (a b : Î±) : a + b = a âŠ” b :=
  IdemSemiring.add_eq_sup _ _
#align add_eq_sup add_eq_sup

-- Porting note: This simp theorem often leads to timeout when `Î±` has rich structure.
--               So, this theorem should be scoped.
scoped[Computability] attribute [simp] add_eq_sup

theorem add_idem (a : Î±) : a + a = a := by simp
                                           -- ðŸŽ‰ no goals
#align add_idem add_idem

theorem nsmul_eq_self : âˆ€ {n : â„•} (_ : n â‰  0) (a : Î±), n â€¢ a = a
  | 0, h => (h rfl).elim
  | 1, _ => one_nsmul
  | n + 2, _ => fun a â†¦ by rw [succ_nsmul, nsmul_eq_self n.succ_ne_zero, add_idem]
                           -- ðŸŽ‰ no goals
#align nsmul_eq_self nsmul_eq_self

theorem add_eq_left_iff_le : a + b = a â†” b â‰¤ a := by simp
                                                     -- ðŸŽ‰ no goals
#align add_eq_left_iff_le add_eq_left_iff_le

theorem add_eq_right_iff_le : a + b = b â†” a â‰¤ b := by simp
                                                      -- ðŸŽ‰ no goals
#align add_eq_right_iff_le add_eq_right_iff_le

alias âŸ¨_, LE.le.add_eq_leftâŸ© := add_eq_left_iff_le
#align has_le.le.add_eq_left LE.le.add_eq_left

alias âŸ¨_, LE.le.add_eq_rightâŸ© := add_eq_right_iff_le
#align has_le.le.add_eq_right LE.le.add_eq_right

theorem add_le_iff : a + b â‰¤ c â†” a â‰¤ c âˆ§ b â‰¤ c := by simp
                                                     -- ðŸŽ‰ no goals
#align add_le_iff add_le_iff

theorem add_le (ha : a â‰¤ c) (hb : b â‰¤ c) : a + b â‰¤ c :=
  add_le_iff.2 âŸ¨ha, hbâŸ©
#align add_le add_le

-- See note [lower instance priority]
instance (priority := 100) IdemSemiring.toCanonicallyOrderedAddMonoid :
    CanonicallyOrderedAddMonoid Î± :=
  { â€¹IdemSemiring Î±â€º with
    add_le_add_left := fun a b hbc c â†¦ by
      simp_rw [add_eq_sup]
      -- âŠ¢ c âŠ” a â‰¤ c âŠ” b
      exact sup_le_sup_left hbc _
      -- ðŸŽ‰ no goals
    exists_add_of_le := fun h â†¦ âŸ¨_, h.add_eq_right.symmâŸ©
    le_self_add := fun a b â†¦ add_eq_right_iff_le.1 <| by rw [â† add_assoc, add_idem] }
                                                         -- ðŸŽ‰ no goals
#align idem_semiring.to_canonically_ordered_add_monoid IdemSemiring.toCanonicallyOrderedAddMonoid

-- See note [lower instance priority]
instance (priority := 100) IdemSemiring.toCovariantClass_mul_le :
    CovariantClass Î± Î± (Â· * Â·) (Â· â‰¤ Â·) :=
  âŸ¨fun a b c hbc â†¦ add_eq_left_iff_le.1 <| by rw [â† mul_add, hbc.add_eq_left]âŸ©
                                              -- ðŸŽ‰ no goals
#align idem_semiring.to_covariant_class_mul_le IdemSemiring.toCovariantClass_mul_le

-- See note [lower instance priority]
instance (priority := 100) IdemSemiring.toCovariantClass_swap_mul_le :
    CovariantClass Î± Î± (swap (Â· * Â·)) (Â· â‰¤ Â·) :=
  âŸ¨fun a b c hbc â†¦ add_eq_left_iff_le.1 <| by rw [â† add_mul, hbc.add_eq_left]âŸ©
                                              -- ðŸŽ‰ no goals
#align idem_semiring.to_covariant_class_swap_mul_le IdemSemiring.toCovariantClass_swap_mul_le

end IdemSemiring

section KleeneAlgebra

variable [KleeneAlgebra Î±] {a b c : Î±}

@[simp]
theorem one_le_kstar : 1 â‰¤ aâˆ— :=
  KleeneAlgebra.one_le_kstar _
#align one_le_kstar one_le_kstar

theorem mul_kstar_le_kstar : a * aâˆ— â‰¤ aâˆ— :=
  KleeneAlgebra.mul_kstar_le_kstar _
#align mul_kstar_le_kstar mul_kstar_le_kstar

theorem kstar_mul_le_kstar : aâˆ— * a â‰¤ aâˆ— :=
  KleeneAlgebra.kstar_mul_le_kstar _
#align kstar_mul_le_kstar kstar_mul_le_kstar

theorem mul_kstar_le_self : b * a â‰¤ b â†’ b * aâˆ— â‰¤ b :=
  KleeneAlgebra.mul_kstar_le_self _ _
#align mul_kstar_le_self mul_kstar_le_self

theorem kstar_mul_le_self : a * b â‰¤ b â†’ aâˆ— * b â‰¤ b :=
  KleeneAlgebra.kstar_mul_le_self _ _
#align kstar_mul_le_self kstar_mul_le_self

theorem mul_kstar_le (hb : b â‰¤ c) (ha : c * a â‰¤ c) : b * aâˆ— â‰¤ c :=
  (mul_le_mul_right' hb _).trans <| mul_kstar_le_self ha
#align mul_kstar_le mul_kstar_le

theorem kstar_mul_le (hb : b â‰¤ c) (ha : a * c â‰¤ c) : aâˆ— * b â‰¤ c :=
  (mul_le_mul_left' hb _).trans <| kstar_mul_le_self ha
#align kstar_mul_le kstar_mul_le

theorem kstar_le_of_mul_le_left (hb : 1 â‰¤ b) : b * a â‰¤ b â†’ aâˆ— â‰¤ b := by
  simpa using mul_kstar_le hb
  -- ðŸŽ‰ no goals
#align kstar_le_of_mul_le_left kstar_le_of_mul_le_left

theorem kstar_le_of_mul_le_right (hb : 1 â‰¤ b) : a * b â‰¤ b â†’ aâˆ— â‰¤ b := by
  simpa using kstar_mul_le hb
  -- ðŸŽ‰ no goals
#align kstar_le_of_mul_le_right kstar_le_of_mul_le_right

@[simp]
theorem le_kstar : a â‰¤ aâˆ— :=
  le_trans (le_mul_of_one_le_left' one_le_kstar) kstar_mul_le_kstar
#align le_kstar le_kstar

@[mono]
theorem kstar_mono : Monotone (KStar.kstar : Î± â†’ Î±) :=
  fun _ _ h â†¦
    kstar_le_of_mul_le_left one_le_kstar <| kstar_mul_le (h.trans le_kstar) <| mul_kstar_le_kstar
#align kstar_mono kstar_mono

@[simp]
theorem kstar_eq_one : aâˆ— = 1 â†” a â‰¤ 1 :=
  âŸ¨le_kstar.trans_eq,
    fun h â†¦ one_le_kstar.antisymm' <| kstar_le_of_mul_le_left le_rfl <| by rwa [one_mul]âŸ©
                                                                           -- ðŸŽ‰ no goals
#align kstar_eq_one kstar_eq_one

@[simp]
theorem kstar_zero : (0 : Î±)âˆ— = 1 :=
  kstar_eq_one.2 zero_le_one
#align kstar_zero kstar_zero

@[simp]
theorem kstar_one : (1 : Î±)âˆ— = 1 :=
  kstar_eq_one.2 le_rfl
#align kstar_one kstar_one

@[simp]
theorem kstar_mul_kstar (a : Î±) : aâˆ— * aâˆ— = aâˆ— :=
  (mul_kstar_le le_rfl <| kstar_mul_le_kstar).antisymm <| le_mul_of_one_le_left' one_le_kstar
#align kstar_mul_kstar kstar_mul_kstar

@[simp]
theorem kstar_eq_self : aâˆ— = a â†” a * a = a âˆ§ 1 â‰¤ a :=
  âŸ¨fun h â†¦ âŸ¨by rw [â† h, kstar_mul_kstar], one_le_kstar.trans_eq hâŸ©,
               -- ðŸŽ‰ no goals
    fun h â†¦ (kstar_le_of_mul_le_left h.2 h.1.le).antisymm le_kstarâŸ©
#align kstar_eq_self kstar_eq_self

@[simp]
theorem kstar_idem (a : Î±) : aâˆ—âˆ— = aâˆ— :=
  kstar_eq_self.2 âŸ¨kstar_mul_kstar _, one_le_kstarâŸ©
#align kstar_idem kstar_idem

@[simp]
theorem pow_le_kstar : âˆ€ {n : â„•}, a ^ n â‰¤ aâˆ—
  | 0 => (pow_zero _).trans_le one_le_kstar
  | n + 1 => by
    rw [pow_succ]
    -- âŠ¢ a * a ^ n â‰¤ aâˆ—
    exact (mul_le_mul_left' pow_le_kstar _).trans mul_kstar_le_kstar
    -- ðŸŽ‰ no goals
#align pow_le_kstar pow_le_kstar

end KleeneAlgebra

namespace Prod

instance instIdemSemiring [IdemSemiring Î±] [IdemSemiring Î²] : IdemSemiring (Î± Ã— Î²) :=
  { Prod.instSemiring, Prod.semilatticeSup _ _, Prod.orderBot _ _ with
    add_eq_sup := fun _ _ â†¦ ext (add_eq_sup _ _) (add_eq_sup _ _) }

instance [IdemCommSemiring Î±] [IdemCommSemiring Î²] : IdemCommSemiring (Î± Ã— Î²) :=
  { Prod.instCommSemiring, Prod.instIdemSemiring with }

variable [KleeneAlgebra Î±] [KleeneAlgebra Î²]

instance : KleeneAlgebra (Î± Ã— Î²) :=
  { Prod.instIdemSemiring with
    kstar := fun a â†¦ (a.1âˆ—, a.2âˆ—)
    one_le_kstar := fun _ â†¦ âŸ¨one_le_kstar, one_le_kstarâŸ©
    mul_kstar_le_kstar := fun _ â†¦ âŸ¨mul_kstar_le_kstar, mul_kstar_le_kstarâŸ©
    kstar_mul_le_kstar := fun _ â†¦ âŸ¨kstar_mul_le_kstar, kstar_mul_le_kstarâŸ©
    mul_kstar_le_self := fun _ _ â†¦ And.imp mul_kstar_le_self mul_kstar_le_self
    kstar_mul_le_self := fun _ _ â†¦ And.imp kstar_mul_le_self kstar_mul_le_self }

theorem kstar_def (a : Î± Ã— Î²) : aâˆ— = (a.1âˆ—, a.2âˆ—) :=
  rfl
#align prod.kstar_def Prod.kstar_def

@[simp]
theorem fst_kstar (a : Î± Ã— Î²) : aâˆ—.1 = a.1âˆ— :=
  rfl
#align prod.fst_kstar Prod.fst_kstar

@[simp]
theorem snd_kstar (a : Î± Ã— Î²) : aâˆ—.2 = a.2âˆ— :=
  rfl
#align prod.snd_kstar Prod.snd_kstar

end Prod

namespace Pi

instance instIdemSemiring [âˆ€ i, IdemSemiring (Ï€ i)] : IdemSemiring (âˆ€ i, Ï€ i) :=
  { Pi.semiring, Pi.semilatticeSup, Pi.orderBot with
    add_eq_sup := fun _ _ â†¦ funext fun _ â†¦ add_eq_sup _ _ }

instance [âˆ€ i, IdemCommSemiring (Ï€ i)] : IdemCommSemiring (âˆ€ i, Ï€ i) :=
  { Pi.commSemiring, Pi.instIdemSemiring with }

variable [âˆ€ i, KleeneAlgebra (Ï€ i)]

instance : KleeneAlgebra (âˆ€ i, Ï€ i) :=
  { Pi.instIdemSemiring with
    kstar := fun a i â†¦ (a i)âˆ—
    one_le_kstar := fun _ _ â†¦ one_le_kstar
    mul_kstar_le_kstar := fun _ _ â†¦ mul_kstar_le_kstar
    kstar_mul_le_kstar := fun _ _ â†¦ kstar_mul_le_kstar
    mul_kstar_le_self := fun _ _ h _ â†¦ mul_kstar_le_self <| h _
    kstar_mul_le_self := fun _ _ h _ â†¦ kstar_mul_le_self <| h _ }

theorem kstar_def (a : âˆ€ i, Ï€ i) : aâˆ— = fun i â†¦ (a i)âˆ— :=
  rfl
#align pi.kstar_def Pi.kstar_def

@[simp]
theorem kstar_apply (a : âˆ€ i, Ï€ i) (i : Î¹) : aâˆ— i = (a i)âˆ— :=
  rfl
#align pi.kstar_apply Pi.kstar_apply

end Pi

namespace Function.Injective

-- See note [reducible non-instances]
/-- Pullback an `IdemSemiring` instance along an injective function. -/
@[reducible]
protected def idemSemiring [IdemSemiring Î±] [Zero Î²] [One Î²] [Add Î²] [Mul Î²] [Pow Î² â„•] [SMul â„• Î²]
    [NatCast Î²] [Sup Î²] [Bot Î²] (f : Î² â†’ Î±) (hf : Injective f) (zero : f 0 = 0) (one : f 1 = 1)
    (add : âˆ€ x y, f (x + y) = f x + f y) (mul : âˆ€ x y, f (x * y) = f x * f y)
    (nsmul : âˆ€ (x) (n : â„•), f (n â€¢ x) = n â€¢ f x) (npow : âˆ€ (x) (n : â„•), f (x ^ n) = f x ^ n)
    (nat_cast : âˆ€ n : â„•, f n = n) (sup : âˆ€ a b, f (a âŠ” b) = f a âŠ” f b) (bot : f âŠ¥ = âŠ¥) :
    IdemSemiring Î² :=
  { hf.semiring f zero one add mul nsmul npow nat_cast, hf.semilatticeSup _ sup,
    â€¹Bot Î²â€º with
    add_eq_sup := fun a b â†¦ hf <| by erw [sup, add, add_eq_sup]
                                     -- ðŸŽ‰ no goals
    bot := âŠ¥
    bot_le := fun a â†¦ bot.trans_le <| @bot_le _ _ _ <| f a }
#align function.injective.idem_semiring Function.Injective.idemSemiring

-- See note [reducible non-instances]
/-- Pullback an `IdemCommSemiring` instance along an injective function. -/
@[reducible]
protected def idemCommSemiring [IdemCommSemiring Î±] [Zero Î²] [One Î²] [Add Î²] [Mul Î²] [Pow Î² â„•]
    [SMul â„• Î²] [NatCast Î²] [Sup Î²] [Bot Î²] (f : Î² â†’ Î±) (hf : Injective f) (zero : f 0 = 0)
    (one : f 1 = 1) (add : âˆ€ x y, f (x + y) = f x + f y) (mul : âˆ€ x y, f (x * y) = f x * f y)
    (nsmul : âˆ€ (x) (n : â„•), f (n â€¢ x) = n â€¢ f x) (npow : âˆ€ (x) (n : â„•), f (x ^ n) = f x ^ n)
    (nat_cast : âˆ€ n : â„•, f n = n) (sup : âˆ€ a b, f (a âŠ” b) = f a âŠ” f b) (bot : f âŠ¥ = âŠ¥) :
    IdemCommSemiring Î² :=
  { hf.commSemiring f zero one add mul nsmul npow nat_cast,
    hf.idemSemiring f zero one add mul nsmul npow nat_cast sup bot with }
#align function.injective.idem_comm_semiring Function.Injective.idemCommSemiring

-- See note [reducible non-instances]
/-- Pullback a `KleeneAlgebra` instance along an injective function. -/
@[reducible]
protected def kleeneAlgebra [KleeneAlgebra Î±] [Zero Î²] [One Î²] [Add Î²] [Mul Î²] [Pow Î² â„•] [SMul â„• Î²]
    [NatCast Î²] [Sup Î²] [Bot Î²] [KStar Î²] (f : Î² â†’ Î±) (hf : Injective f) (zero : f 0 = 0)
    (one : f 1 = 1) (add : âˆ€ x y, f (x + y) = f x + f y) (mul : âˆ€ x y, f (x * y) = f x * f y)
    (nsmul : âˆ€ (x) (n : â„•), f (n â€¢ x) = n â€¢ f x) (npow : âˆ€ (x) (n : â„•), f (x ^ n) = f x ^ n)
    (nat_cast : âˆ€ n : â„•, f n = n) (sup : âˆ€ a b, f (a âŠ” b) = f a âŠ” f b) (bot : f âŠ¥ = âŠ¥)
    (kstar : âˆ€ a, f aâˆ— = (f a)âˆ—) : KleeneAlgebra Î² :=
  { hf.idemSemiring f zero one add mul nsmul npow nat_cast sup bot,
    â€¹KStar Î²â€º with
    one_le_kstar := fun a â†¦ one.trans_le <| by
      erw [kstar]
      -- âŠ¢ 1 â‰¤ (f a)âˆ—
      exact one_le_kstar
      -- ðŸŽ‰ no goals
    mul_kstar_le_kstar := fun a â†¦ by
      change f _ â‰¤ _
      -- âŠ¢ f (a * aâˆ—) â‰¤ f aâˆ—
      erw [mul, kstar]
      -- âŠ¢ f a * (f a)âˆ— â‰¤ (f a)âˆ—
      exact mul_kstar_le_kstar
      -- ðŸŽ‰ no goals
    kstar_mul_le_kstar := fun a â†¦ by
      change f _ â‰¤ _
      -- âŠ¢ f (aâˆ— * a) â‰¤ f aâˆ—
      erw [mul, kstar]
      -- âŠ¢ (f a)âˆ— * f a â‰¤ (f a)âˆ—
      exact kstar_mul_le_kstar
      -- ðŸŽ‰ no goals
    mul_kstar_le_self := fun a b (h : f _ â‰¤ _) â†¦ by
      change f _ â‰¤ _
      -- âŠ¢ f (b * aâˆ—) â‰¤ f b
      erw [mul, kstar]
      -- âŠ¢ f b * (f a)âˆ— â‰¤ f b
      erw [mul] at h
      -- âŠ¢ f b * (f a)âˆ— â‰¤ f b
      exact mul_kstar_le_self h
      -- ðŸŽ‰ no goals
    kstar_mul_le_self := fun a b (h : f _ â‰¤ _) â†¦ by
      change f _ â‰¤ _
      -- âŠ¢ f (aâˆ— * b) â‰¤ f b
      erw [mul, kstar]
      -- âŠ¢ (f a)âˆ— * f b â‰¤ f b
      erw [mul] at h
      -- âŠ¢ (f a)âˆ— * f b â‰¤ f b
      exact kstar_mul_le_self h }
      -- ðŸŽ‰ no goals
#align function.injective.kleene_algebra Function.Injective.kleeneAlgebra

end Function.Injective
