/-
Copyright (c) 2020 Fr√©d√©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fr√©d√©ric Dupuis
-/
import Mathlib.Algebra.Module.Pi
import Mathlib.Algebra.Module.Prod
import Mathlib.Algebra.Order.Monoid.Prod
import Mathlib.Algebra.Order.Pi
import Mathlib.Data.Set.Pointwise.SMul
import Mathlib.Tactic.GCongr.Core
import Mathlib.Tactic.Positivity

#align_import algebra.order.smul from "leanprover-community/mathlib"@"9003f28797c0664a49e4179487267c494477d853"

/-!
# Ordered scalar product

In this file we define

* `OrderedSMul R M` : an ordered additive commutative monoid `M` is an `OrderedSMul`
  over an `OrderedSemiring` `R` if the scalar product respects the order relation on the
  monoid and on the ring. There is a correspondence between this structure and convex cones,
  which is proven in `Analysis/Convex/Cone.lean`.

## Implementation notes
* We choose to define `OrderedSMul` as a `Prop`-valued mixin, so that it can be
  used for actions, modules, and algebras
  (the axioms for an "ordered algebra" are exactly that the algebra is ordered as a module).
* To get ordered modules and ordered vector spaces, it suffices to replace the
  `OrderedAddCommMonoid` and the `OrderedSemiring` as desired.

## References

* https://en.wikipedia.org/wiki/Ordered_module

## Tags

ordered module, ordered scalar, ordered smul, ordered action, ordered vector space
-/


open Pointwise

/-- The ordered scalar product property is when an ordered additive commutative monoid
with a partial order has a scalar multiplication which is compatible with the order.
-/
class OrderedSMul (R M : Type*) [OrderedSemiring R] [OrderedAddCommMonoid M] [SMulWithZero R M] :
  Prop where
  /-- Scalar multiplication by positive elements preserves the order. -/
  protected smul_lt_smul_of_pos : ‚àÄ {a b : M}, ‚àÄ {c : R}, a < b ‚Üí 0 < c ‚Üí c ‚Ä¢ a < c ‚Ä¢ b
  /-- If `c ‚Ä¢ a < c ‚Ä¢ b` for some positive `c`, then `a < b`. -/
  protected lt_of_smul_lt_smul_of_pos : ‚àÄ {a b : M}, ‚àÄ {c : R}, c ‚Ä¢ a < c ‚Ä¢ b ‚Üí 0 < c ‚Üí a < b
#align ordered_smul OrderedSMul

variable {Œπ ùïú R M N : Type*}

namespace OrderDual

instance instSMulWithZeroOrderDual [Zero R] [AddZeroClass M] [SMulWithZero R M] :
    SMulWithZero R M·µí·µà :=
  { instSMulOrderDual with
    zero_smul := fun m => OrderDual.rec (zero_smul _) m
    smul_zero := fun r => OrderDual.rec (@smul_zero R M _ _) r }

instance instMulActionOrderDual [Monoid R] [MulAction R M] : MulAction R M·µí·µà :=
  { instSMulOrderDual with
    one_smul := fun m => OrderDual.rec (one_smul _) m
    mul_smul := fun r => OrderDual.rec (@mul_smul R M _ _) r }

instance [MonoidWithZero R] [AddMonoid M] [MulActionWithZero R M] : MulActionWithZero R M·µí·µà :=
  { instMulActionOrderDual, instSMulWithZeroOrderDual with }

instance [MonoidWithZero R] [AddMonoid M] [DistribMulAction R M] : DistribMulAction R M·µí·µà where
  smul_add _ a := OrderDual.rec (fun _ b => OrderDual.rec (smul_add _ _) b) a
  smul_zero r := OrderDual.rec (@smul_zero _ M _ _) r

instance [OrderedSemiring R] [OrderedAddCommMonoid M] [SMulWithZero R M] [OrderedSMul R M] :
    OrderedSMul R M·µí·µà where
  smul_lt_smul_of_pos {a b} := @OrderedSMul.smul_lt_smul_of_pos R M _ _ _ _ b a
  lt_of_smul_lt_smul_of_pos {a b} := @OrderedSMul.lt_of_smul_lt_smul_of_pos R M _ _ _ _ b a

end OrderDual

section OrderedSMul

variable [OrderedSemiring R] [OrderedAddCommMonoid M] [SMulWithZero R M] [OrderedSMul R M]
  {s : Set M} {a b : M} {c : R}

@[gcongr] theorem smul_lt_smul_of_pos : a < b ‚Üí 0 < c ‚Üí c ‚Ä¢ a < c ‚Ä¢ b :=
  OrderedSMul.smul_lt_smul_of_pos
#align smul_lt_smul_of_pos smul_lt_smul_of_pos

@[gcongr] theorem smul_le_smul_of_nonneg (h‚ÇÅ : a ‚â§ b) (h‚ÇÇ : 0 ‚â§ c) : c ‚Ä¢ a ‚â§ c ‚Ä¢ b := by
  rcases h‚ÇÅ.eq_or_lt with (rfl | hab)
  ¬∑ rfl
  ¬∑ rcases h‚ÇÇ.eq_or_lt with (rfl | hc)
    ¬∑ rw [zero_smul, zero_smul]
    ¬∑ exact (smul_lt_smul_of_pos hab hc).le
#align smul_le_smul_of_nonneg smul_le_smul_of_nonneg

theorem smul_nonneg (hc : 0 ‚â§ c) (ha : 0 ‚â§ a) : 0 ‚â§ c ‚Ä¢ a :=
  calc
    (0 : M) = c ‚Ä¢ (0 : M) := (smul_zero c).symm
    _ ‚â§ c ‚Ä¢ a := smul_le_smul_of_nonneg ha hc
#align smul_nonneg smul_nonneg

theorem smul_nonpos_of_nonneg_of_nonpos (hc : 0 ‚â§ c) (ha : a ‚â§ 0) : c ‚Ä¢ a ‚â§ 0 :=
  @smul_nonneg R M·µí·µà _ _ _ _ _ _ hc ha
#align smul_nonpos_of_nonneg_of_nonpos smul_nonpos_of_nonneg_of_nonpos

theorem eq_of_smul_eq_smul_of_pos_of_le (h‚ÇÅ : c ‚Ä¢ a = c ‚Ä¢ b) (hc : 0 < c) (hle : a ‚â§ b) : a = b :=
  hle.lt_or_eq.resolve_left fun hlt => (smul_lt_smul_of_pos hlt hc).ne h‚ÇÅ
#align eq_of_smul_eq_smul_of_pos_of_le eq_of_smul_eq_smul_of_pos_of_le

theorem lt_of_smul_lt_smul_of_nonneg (h : c ‚Ä¢ a < c ‚Ä¢ b) (hc : 0 ‚â§ c) : a < b :=
  hc.eq_or_lt.elim
    (fun hc => False.elim <| lt_irrefl (0 : M) <| by rwa [‚Üê hc, zero_smul, zero_smul] at h)
    (OrderedSMul.lt_of_smul_lt_smul_of_pos h)
#align lt_of_smul_lt_smul_of_nonneg lt_of_smul_lt_smul_of_nonneg

theorem smul_lt_smul_iff_of_pos (hc : 0 < c) : c ‚Ä¢ a < c ‚Ä¢ b ‚Üî a < b :=
  ‚ü®fun h => lt_of_smul_lt_smul_of_nonneg h hc.le, fun h => smul_lt_smul_of_pos h hc‚ü©
#align smul_lt_smul_iff_of_pos smul_lt_smul_iff_of_pos

theorem smul_pos_iff_of_pos (hc : 0 < c) : 0 < c ‚Ä¢ a ‚Üî 0 < a :=
  calc
    0 < c ‚Ä¢ a ‚Üî c ‚Ä¢ (0 : M) < c ‚Ä¢ a := by rw [smul_zero]
    _ ‚Üî 0 < a := smul_lt_smul_iff_of_pos hc
#align smul_pos_iff_of_pos smul_pos_iff_of_pos

alias ‚ü®_, smul_pos‚ü© := smul_pos_iff_of_pos
#align smul_pos smul_pos

theorem monotone_smul_left (hc : 0 ‚â§ c) : Monotone (SMul.smul c : M ‚Üí M) := fun _ _ h =>
  smul_le_smul_of_nonneg h hc
#align monotone_smul_left monotone_smul_left

theorem strictMono_smul_left (hc : 0 < c) : StrictMono (SMul.smul c : M ‚Üí M) := fun _ _ h =>
  smul_lt_smul_of_pos h hc
#align strict_mono_smul_left strictMono_smul_left

theorem smul_lowerBounds_subset_lowerBounds_smul (hc : 0 ‚â§ c) :
    c ‚Ä¢ lowerBounds s ‚äÜ lowerBounds (c ‚Ä¢ s) :=
  (monotone_smul_left hc).image_lowerBounds_subset_lowerBounds_image
#align smul_lower_bounds_subset_lower_bounds_smul smul_lowerBounds_subset_lowerBounds_smul

theorem smul_upperBounds_subset_upperBounds_smul (hc : 0 ‚â§ c) :
    c ‚Ä¢ upperBounds s ‚äÜ upperBounds (c ‚Ä¢ s) :=
  (monotone_smul_left hc).image_upperBounds_subset_upperBounds_image
#align smul_upper_bounds_subset_upper_bounds_smul smul_upperBounds_subset_upperBounds_smul

theorem BddBelow.smul_of_nonneg (hs : BddBelow s) (hc : 0 ‚â§ c) : BddBelow (c ‚Ä¢ s) :=
  (monotone_smul_left hc).map_bddBelow hs
#align bdd_below.smul_of_nonneg BddBelow.smul_of_nonneg

theorem BddAbove.smul_of_nonneg (hs : BddAbove s) (hc : 0 ‚â§ c) : BddAbove (c ‚Ä¢ s) :=
  (monotone_smul_left hc).map_bddAbove hs
#align bdd_above.smul_of_nonneg BddAbove.smul_of_nonneg

end OrderedSMul

/-- To prove that a linear ordered monoid is an ordered module, it suffices to verify only the first
axiom of `OrderedSMul`. -/
theorem OrderedSMul.mk'' [OrderedSemiring ùïú] [LinearOrderedAddCommMonoid M] [SMulWithZero ùïú M]
    (h : ‚àÄ ‚¶Éc : ùïú‚¶Ñ, 0 < c ‚Üí StrictMono fun a : M => c ‚Ä¢ a) : OrderedSMul ùïú M :=
  { smul_lt_smul_of_pos := fun hab hc => h hc hab
    lt_of_smul_lt_smul_of_pos := fun hab hc => (h hc).lt_iff_lt.1 hab }
#align ordered_smul.mk'' OrderedSMul.mk''

instance Nat.orderedSMul [LinearOrderedCancelAddCommMonoid M] : OrderedSMul ‚Ñï M :=
  OrderedSMul.mk'' fun n hn a b hab => by
    cases n with
    | zero => cases hn
    | succ n =>
      induction n with
      | zero => dsimp; rwa [one_nsmul, one_nsmul]
      | succ n ih => simp only [succ_nsmul _ n.succ, _root_.add_lt_add hab (ih n.succ_pos)]
#align nat.ordered_smul Nat.orderedSMul

instance Int.orderedSMul [LinearOrderedAddCommGroup M] : OrderedSMul ‚Ñ§ M :=
  OrderedSMul.mk'' fun n hn => by
    cases n
    ¬∑ simp only [Int.ofNat_eq_coe, Int.coe_nat_pos, coe_nat_zsmul] at hn ‚ä¢
      exact strictMono_smul_left hn
    ¬∑ cases (Int.negSucc_not_pos _).1 hn
#align int.ordered_smul Int.orderedSMul

-- TODO: `LinearOrderedField M ‚Üí OrderedSMul ‚Ñö M`
instance LinearOrderedSemiring.toOrderedSMul {R : Type*} [LinearOrderedSemiring R] :
    OrderedSMul R R :=
  OrderedSMul.mk'' fun _ => strictMono_mul_left_of_pos
#align linear_ordered_semiring.to_ordered_smul LinearOrderedSemiring.toOrderedSMul

section LinearOrderedSemifield

variable [LinearOrderedSemifield ùïú] [OrderedAddCommMonoid M] [OrderedAddCommMonoid N]
  [MulActionWithZero ùïú M] [MulActionWithZero ùïú N]

/-- To prove that a vector space over a linear ordered field is ordered, it suffices to verify only
the first axiom of `OrderedSMul`. -/
theorem OrderedSMul.mk' (h : ‚àÄ ‚¶Éa b : M‚¶Ñ ‚¶Éc : ùïú‚¶Ñ, a < b ‚Üí 0 < c ‚Üí c ‚Ä¢ a ‚â§ c ‚Ä¢ b) :
    OrderedSMul ùïú M := by
  have hlt' : ‚àÄ (a b : M) (c : ùïú), a < b ‚Üí 0 < c ‚Üí c ‚Ä¢ a < c ‚Ä¢ b := by
    refine' fun a b c hab hc => (h hab hc).lt_of_ne _
    rw [Ne.def, hc.ne'.isUnit.smul_left_cancel]
    exact hab.ne
  refine' { smul_lt_smul_of_pos := fun {a b c} => hlt' a b c..}
  intro a b c hab hc
  obtain ‚ü®c, rfl‚ü© := hc.ne'.isUnit
  rw [‚Üê inv_smul_smul c a, ‚Üê inv_smul_smul c b]
  refine' hlt' _ _ _ hab (pos_of_mul_pos_right _ hc.le)
  simp only [c.mul_inv, zero_lt_one]
#align ordered_smul.mk' OrderedSMul.mk'

instance [OrderedSMul ùïú M] [OrderedSMul ùïú N] : OrderedSMul ùïú (M √ó N) :=
  OrderedSMul.mk' fun _ _ _ h hc =>
    ‚ü®smul_le_smul_of_nonneg h.1.1 hc.le, smul_le_smul_of_nonneg h.1.2 hc.le‚ü©

instance Pi.orderedSMul {M : Œπ ‚Üí Type*} [‚àÄ i, OrderedAddCommMonoid (M i)]
    [‚àÄ i, MulActionWithZero ùïú (M i)] [‚àÄ i, OrderedSMul ùïú (M i)] : OrderedSMul ùïú (‚àÄ i, M i) :=
  OrderedSMul.mk' fun _ _ _ h hc i => smul_le_smul_of_nonneg (h.le i) hc.le
#align pi.ordered_smul Pi.orderedSMul

/- Sometimes Lean fails to apply the dependent version to non-dependent functions, so we define
another instance. -/
instance Pi.orderedSMul' [OrderedSMul ùïú M] : OrderedSMul ùïú (Œπ ‚Üí M) :=
  Pi.orderedSMul
#align pi.ordered_smul' Pi.orderedSMul'

-- Sometimes Lean fails to unify the module with the scalars, so we define another instance.
instance Pi.orderedSMul'' : OrderedSMul ùïú (Œπ ‚Üí ùïú) :=
  @Pi.orderedSMul' Œπ ùïú ùïú _ _ _ _
#align pi.ordered_smul'' Pi.orderedSMul''

variable [OrderedSMul ùïú M] {s : Set M} {a b : M} {c : ùïú}

theorem smul_le_smul_iff_of_pos (hc : 0 < c) : c ‚Ä¢ a ‚â§ c ‚Ä¢ b ‚Üî a ‚â§ b :=
  ‚ü®fun h =>
    inv_smul_smul‚ÇÄ hc.ne' a ‚ñ∏
      inv_smul_smul‚ÇÄ hc.ne' b ‚ñ∏ smul_le_smul_of_nonneg h (inv_nonneg.2 hc.le),
    fun h => smul_le_smul_of_nonneg h hc.le‚ü©
#align smul_le_smul_iff_of_pos smul_le_smul_iff_of_pos

theorem inv_smul_le_iff (h : 0 < c) : c‚Åª¬π ‚Ä¢ a ‚â§ b ‚Üî a ‚â§ c ‚Ä¢ b := by
  rw [‚Üê smul_le_smul_iff_of_pos h, smul_inv_smul‚ÇÄ h.ne']
#align inv_smul_le_iff inv_smul_le_iff

theorem inv_smul_lt_iff (h : 0 < c) : c‚Åª¬π ‚Ä¢ a < b ‚Üî a < c ‚Ä¢ b := by
  rw [‚Üê smul_lt_smul_iff_of_pos h, smul_inv_smul‚ÇÄ h.ne']
#align inv_smul_lt_iff inv_smul_lt_iff

theorem le_inv_smul_iff (h : 0 < c) : a ‚â§ c‚Åª¬π ‚Ä¢ b ‚Üî c ‚Ä¢ a ‚â§ b := by
  rw [‚Üê smul_le_smul_iff_of_pos h, smul_inv_smul‚ÇÄ h.ne']
#align le_inv_smul_iff le_inv_smul_iff

theorem lt_inv_smul_iff (h : 0 < c) : a < c‚Åª¬π ‚Ä¢ b ‚Üî c ‚Ä¢ a < b := by
  rw [‚Üê smul_lt_smul_iff_of_pos h, smul_inv_smul‚ÇÄ h.ne']
#align lt_inv_smul_iff lt_inv_smul_iff

variable (M)

/-- Left scalar multiplication as an order isomorphism. -/
@[simps]
def OrderIso.smulLeft (hc : 0 < c) : M ‚âÉo M where
  toFun b := c ‚Ä¢ b
  invFun b := c‚Åª¬π ‚Ä¢ b
  left_inv := inv_smul_smul‚ÇÄ hc.ne'
  right_inv := smul_inv_smul‚ÇÄ hc.ne'
  map_rel_iff' := smul_le_smul_iff_of_pos hc
#align order_iso.smul_left OrderIso.smulLeft
#align order_iso.smul_left_symm_apply OrderIso.smulLeft_symm_apply
#align order_iso.smul_left_apply OrderIso.smulLeft_apply

variable {M}

@[simp]
theorem lowerBounds_smul_of_pos (hc : 0 < c) : lowerBounds (c ‚Ä¢ s) = c ‚Ä¢ lowerBounds s :=
  (OrderIso.smulLeft _ hc).lowerBounds_image
#align lower_bounds_smul_of_pos lowerBounds_smul_of_pos

@[simp]
theorem upperBounds_smul_of_pos (hc : 0 < c) : upperBounds (c ‚Ä¢ s) = c ‚Ä¢ upperBounds s :=
  (OrderIso.smulLeft _ hc).upperBounds_image
#align upper_bounds_smul_of_pos upperBounds_smul_of_pos

@[simp]
theorem bddBelow_smul_iff_of_pos (hc : 0 < c) : BddBelow (c ‚Ä¢ s) ‚Üî BddBelow s :=
  (OrderIso.smulLeft _ hc).bddBelow_image
#align bdd_below_smul_iff_of_pos bddBelow_smul_iff_of_pos

@[simp]
theorem bddAbove_smul_iff_of_pos (hc : 0 < c) : BddAbove (c ‚Ä¢ s) ‚Üî BddAbove s :=
  (OrderIso.smulLeft _ hc).bddAbove_image
#align bdd_above_smul_iff_of_pos bddAbove_smul_iff_of_pos

end LinearOrderedSemifield

namespace Tactic

section OrderedSMul

variable [OrderedSemiring R] [OrderedAddCommMonoid M] [SMulWithZero R M] [OrderedSMul R M] {a : R}
  {b : M}

private theorem smul_nonneg_of_pos_of_nonneg (ha : 0 < a) (hb : 0 ‚â§ b) : 0 ‚â§ a ‚Ä¢ b :=
  smul_nonneg ha.le hb

private theorem smul_nonneg_of_nonneg_of_pos (ha : 0 ‚â§ a) (hb : 0 < b) : 0 ‚â§ a ‚Ä¢ b :=
  smul_nonneg ha hb.le

end OrderedSMul

section NoZeroSMulDivisors

variable [Zero R] [Zero M] [SMul R M] [NoZeroSMulDivisors R M] {a : R} {b : M}

private theorem smul_ne_zero_of_pos_of_ne_zero [Preorder R] (ha : 0 < a) (hb : b ‚â† 0) : a ‚Ä¢ b ‚â† 0 :=
  smul_ne_zero ha.ne' hb

private theorem smul_ne_zero_of_ne_zero_of_pos [Preorder M] (ha : a ‚â† 0) (hb : 0 < b) : a ‚Ä¢ b ‚â† 0 :=
  smul_ne_zero ha hb.ne'

end NoZeroSMulDivisors

-- Porting note: Tactic code not ported yet
-- open Positivity

-- -- failed to format: unknown constant 'term.pseudo.antiquot'
-- /--
--       Extension for the `Positivity` tactic: scalar multiplication is
--       nonnegative/positive/nonzero if both sides are. -/
--     @[ positivity ]
--     unsafe
--   def
--     positivity_smul
--     : expr ‚Üí tactic strictness
--     |
--         e @ q( $ ( a ) ‚Ä¢ $ ( b ) )
--         =>
--         do
--           let strictness_a ‚Üê core a
--             let strictness_b ‚Üê core b
--             match
--               strictness_a , strictness_b
--               with
--               | positive pa , positive pb => positive <$> mk_app ` ` smul_pos [ pa , pb ]
--                 |
--                   positive pa , nonnegative pb
--                   =>
--                   nonnegative <$> mk_app ` ` smul_nonneg_of_pos_of_nonneg [ pa , pb ]
--                 |
--                   nonnegative pa , positive pb
--                   =>
--                   nonnegative <$> mk_app ` ` smul_nonneg_of_nonneg_of_pos [ pa , pb ]
--                 |
--                   nonnegative pa , nonnegative pb
--                   =>
--                   nonnegative <$> mk_app ` ` smul_nonneg [ pa , pb ]
--                 |
--                   positive pa , nonzero pb
--                   =>
--                   nonzero <$> to_expr ` `( smul_ne_zero_of_pos_of_ne_zero $ ( pa ) $ ( pb ) )
--                 |
--                   nonzero pa , positive pb
--                   =>
--                   nonzero <$> to_expr ` `( smul_ne_zero_of_ne_zero_of_pos $ ( pa ) $ ( pb ) )
--                 |
--                   nonzero pa , nonzero pb
--                   =>
--                   nonzero <$> to_expr ` `( smul_ne_zero $ ( pa ) $ ( pb ) )
--                 | sa @ _ , sb @ _ => positivity_fail e a b sa sb
--       | e => pp e >>= fail ‚àò format.bracket "The expression `" "` isn't of the form `a ‚Ä¢ b`"
-- #align tactic.positivity_smul Tactic.positivity_smul

end Tactic
