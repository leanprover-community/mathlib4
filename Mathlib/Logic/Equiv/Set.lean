/-
Copyright (c) 2015 Microsoft Corporation. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Leonardo de Moura, Mario Carneiro
-/
import Mathlib.Data.Set.Function
import Mathlib.Logic.Equiv.Defs
import Mathlib.Tactic.Says

#align_import logic.equiv.set from "leanprover-community/mathlib"@"aba57d4d3dae35460225919dcd82fe91355162f9"

/-!
# Equivalences and sets

In this file we provide lemmas linking equivalences to sets.

Some notable definitions are:

* `Equiv.ofInjective`: an injective function is (noncomputably) equivalent to its range.
* `Equiv.setCongr`: two equal sets are equivalent as types.
* `Equiv.Set.union`: a disjoint union of sets is equivalent to their `Sum`.

This file is separate from `Equiv/Basic` such that we do not require the full lattice structure
on sets before defining what an equivalence is.
-/


open Function Set

universe u v w z

variable {Î± : Sort u} {Î² : Sort v} {Î³ : Sort w}

namespace Equiv

@[simp]
theorem range_eq_univ {Î± : Type*} {Î² : Type*} (e : Î± â‰ƒ Î²) : range e = univ :=
  eq_univ_of_forall e.surjective
#align equiv.range_eq_univ Equiv.range_eq_univ

protected theorem image_eq_preimage {Î± Î²} (e : Î± â‰ƒ Î²) (s : Set Î±) : e '' s = e.symm â»Â¹' s :=
  Set.ext fun _ => mem_image_iff_of_inverse e.left_inv e.right_inv
#align equiv.image_eq_preimage Equiv.image_eq_preimage

@[simp 1001]
theorem _root_.Set.mem_image_equiv {Î± Î²} {S : Set Î±} {f : Î± â‰ƒ Î²} {x : Î²} :
    x âˆˆ f '' S â†” f.symm x âˆˆ S :=
  Set.ext_iff.mp (f.image_eq_preimage S) x
#align set.mem_image_equiv Set.mem_image_equiv

/-- Alias for `Equiv.image_eq_preimage` -/
theorem _root_.Set.image_equiv_eq_preimage_symm {Î± Î²} (S : Set Î±) (f : Î± â‰ƒ Î²) :
    f '' S = f.symm â»Â¹' S :=
  f.image_eq_preimage S
#align set.image_equiv_eq_preimage_symm Set.image_equiv_eq_preimage_symm

/-- Alias for `Equiv.image_eq_preimage` -/
theorem _root_.Set.preimage_equiv_eq_image_symm {Î± Î²} (S : Set Î±) (f : Î² â‰ƒ Î±) :
    f â»Â¹' S = f.symm '' S :=
  (f.symm.image_eq_preimage S).symm
#align set.preimage_equiv_eq_image_symm Set.preimage_equiv_eq_image_symm

-- Porting note: increased priority so this fires before `image_subset_iff`
@[simp high]
protected theorem symm_image_subset {Î± Î²} (e : Î± â‰ƒ Î²) (s : Set Î±) (t : Set Î²) :
    e.symm '' t âŠ† s â†” t âŠ† e '' s := by rw [image_subset_iff, e.image_eq_preimage]
#align equiv.subset_image Equiv.symm_image_subset

@[deprecated] alias subset_image := Equiv.symm_image_subset -- deprecated since 2024-01-19

-- Porting note: increased priority so this fires before `image_subset_iff`
@[simp high]
protected theorem subset_symm_image {Î± Î²} (e : Î± â‰ƒ Î²) (s : Set Î±) (t : Set Î²) :
    s âŠ† e.symm '' t â†” e '' s âŠ† t :=
  calc
    s âŠ† e.symm '' t â†” e.symm.symm '' s âŠ† t := by rw [e.symm.symm_image_subset]
    _ â†” e '' s âŠ† t := by rw [e.symm_symm]
#align equiv.subset_image' Equiv.subset_symm_image

@[deprecated] alias subset_image' := Equiv.subset_symm_image -- deprecated since 2024-01-19

@[simp]
theorem symm_image_image {Î± Î²} (e : Î± â‰ƒ Î²) (s : Set Î±) : e.symm '' (e '' s) = s :=
  e.leftInverse_symm.image_image s
#align equiv.symm_image_image Equiv.symm_image_image

theorem eq_image_iff_symm_image_eq {Î± Î²} (e : Î± â‰ƒ Î²) (s : Set Î±) (t : Set Î²) :
    t = e '' s â†” e.symm '' t = s :=
  (e.symm.injective.image_injective.eq_iff' (e.symm_image_image s)).symm
#align equiv.eq_image_iff_symm_image_eq Equiv.eq_image_iff_symm_image_eq

@[simp]
theorem image_symm_image {Î± Î²} (e : Î± â‰ƒ Î²) (s : Set Î²) : e '' (e.symm '' s) = s :=
  e.symm.symm_image_image s
#align equiv.image_symm_image Equiv.image_symm_image

@[simp]
theorem image_preimage {Î± Î²} (e : Î± â‰ƒ Î²) (s : Set Î²) : e '' (e â»Â¹' s) = s :=
  e.surjective.image_preimage s
#align equiv.image_preimage Equiv.image_preimage

@[simp]
theorem preimage_image {Î± Î²} (e : Î± â‰ƒ Î²) (s : Set Î±) : e â»Â¹' (e '' s) = s :=
  e.injective.preimage_image s
#align equiv.preimage_image Equiv.preimage_image

protected theorem image_compl {Î± Î²} (f : Equiv Î± Î²) (s : Set Î±) : f '' sá¶œ = (f '' s)á¶œ :=
  image_compl_eq f.bijective
#align equiv.image_compl Equiv.image_compl

@[simp]
theorem symm_preimage_preimage {Î± Î²} (e : Î± â‰ƒ Î²) (s : Set Î²) : e.symm â»Â¹' (e â»Â¹' s) = s :=
  e.rightInverse_symm.preimage_preimage s
#align equiv.symm_preimage_preimage Equiv.symm_preimage_preimage

@[simp]
theorem preimage_symm_preimage {Î± Î²} (e : Î± â‰ƒ Î²) (s : Set Î±) : e â»Â¹' (e.symm â»Â¹' s) = s :=
  e.leftInverse_symm.preimage_preimage s
#align equiv.preimage_symm_preimage Equiv.preimage_symm_preimage

theorem preimage_subset {Î± Î²} (e : Î± â‰ƒ Î²) (s t : Set Î²) : e â»Â¹' s âŠ† e â»Â¹' t â†” s âŠ† t :=
  e.surjective.preimage_subset_preimage_iff
#align equiv.preimage_subset Equiv.preimage_subset

-- Porting note (#10618): removed `simp` attribute. `simp` can prove it.
theorem image_subset {Î± Î²} (e : Î± â‰ƒ Î²) (s t : Set Î±) : e '' s âŠ† e '' t â†” s âŠ† t :=
  image_subset_image_iff e.injective
#align equiv.image_subset Equiv.image_subset

@[simp]
theorem image_eq_iff_eq {Î± Î²} (e : Î± â‰ƒ Î²) (s t : Set Î±) : e '' s = e '' t â†” s = t :=
  image_eq_image e.injective
#align equiv.image_eq_iff_eq Equiv.image_eq_iff_eq

theorem preimage_eq_iff_eq_image {Î± Î²} (e : Î± â‰ƒ Î²) (s t) : e â»Â¹' s = t â†” s = e '' t :=
  Set.preimage_eq_iff_eq_image e.bijective
#align equiv.preimage_eq_iff_eq_image Equiv.preimage_eq_iff_eq_image

theorem eq_preimage_iff_image_eq {Î± Î²} (e : Î± â‰ƒ Î²) (s t) : s = e â»Â¹' t â†” e '' s = t :=
  Set.eq_preimage_iff_image_eq e.bijective
#align equiv.eq_preimage_iff_image_eq Equiv.eq_preimage_iff_image_eq

lemma setOf_apply_symm_eq_image_setOf {Î± Î²} (e : Î± â‰ƒ Î²) (p : Î± â†’ Prop) :
    {b | p (e.symm b)} = e '' {a | p a} := by
  rw [Equiv.image_eq_preimage, preimage_setOf_eq]

@[simp]
theorem prod_assoc_preimage {Î± Î² Î³} {s : Set Î±} {t : Set Î²} {u : Set Î³} :
    Equiv.prodAssoc Î± Î² Î³ â»Â¹' s Ã—Ë¢ t Ã—Ë¢ u = (s Ã—Ë¢ t) Ã—Ë¢ u := by
  ext
  simp [and_assoc]
#align equiv.prod_assoc_preimage Equiv.prod_assoc_preimage

@[simp]
theorem prod_assoc_symm_preimage {Î± Î² Î³} {s : Set Î±} {t : Set Î²} {u : Set Î³} :
    (Equiv.prodAssoc Î± Î² Î³).symm â»Â¹' (s Ã—Ë¢ t) Ã—Ë¢ u = s Ã—Ë¢ t Ã—Ë¢ u := by
  ext
  simp [and_assoc]
#align equiv.prod_assoc_symm_preimage Equiv.prod_assoc_symm_preimage

-- `@[simp]` doesn't like these lemmas, as it uses `Set.image_congr'` to turn `Equiv.prodAssoc`
-- into a lambda expression and then unfold it.
theorem prod_assoc_image {Î± Î² Î³} {s : Set Î±} {t : Set Î²} {u : Set Î³} :
    Equiv.prodAssoc Î± Î² Î³ '' (s Ã—Ë¢ t) Ã—Ë¢ u = s Ã—Ë¢ t Ã—Ë¢ u := by
  simpa only [Equiv.image_eq_preimage] using prod_assoc_symm_preimage
#align equiv.prod_assoc_image Equiv.prod_assoc_image

theorem prod_assoc_symm_image {Î± Î² Î³} {s : Set Î±} {t : Set Î²} {u : Set Î³} :
    (Equiv.prodAssoc Î± Î² Î³).symm '' s Ã—Ë¢ t Ã—Ë¢ u = (s Ã—Ë¢ t) Ã—Ë¢ u := by
  simpa only [Equiv.image_eq_preimage] using prod_assoc_preimage
#align equiv.prod_assoc_symm_image Equiv.prod_assoc_symm_image

/-- A set `s` in `Î± Ã— Î²` is equivalent to the sigma-type `Î£ x, {y | (x, y) âˆˆ s}`. -/
def setProdEquivSigma {Î± Î² : Type*} (s : Set (Î± Ã— Î²)) :
    s â‰ƒ Î£x : Î±, { y : Î² | (x, y) âˆˆ s } where
  toFun x := âŸ¨x.1.1, x.1.2, by simpâŸ©
  invFun x := âŸ¨(x.1, x.2.1), x.2.2âŸ©
  left_inv := fun âŸ¨âŸ¨x, yâŸ©, hâŸ© => rfl
  right_inv := fun âŸ¨x, y, hâŸ© => rfl
#align equiv.set_prod_equiv_sigma Equiv.setProdEquivSigma

/-- The subtypes corresponding to equal sets are equivalent. -/
@[simps! apply]
def setCongr {Î± : Type*} {s t : Set Î±} (h : s = t) : s â‰ƒ t :=
  subtypeEquivProp h
#align equiv.set_congr Equiv.setCongr
#align equiv.set_congr_apply Equiv.setCongr_apply

-- We could construct this using `Equiv.Set.image e s e.injective`,
-- but this definition provides an explicit inverse.
/-- A set is equivalent to its image under an equivalence.
-/
@[simps]
def image {Î± Î² : Type*} (e : Î± â‰ƒ Î²) (s : Set Î±) :
    s â‰ƒ e '' s where
  toFun x := âŸ¨e x.1, by simpâŸ©
  invFun y :=
    âŸ¨e.symm y.1, by
      rcases y with âŸ¨-, âŸ¨a, âŸ¨m, rflâŸ©âŸ©âŸ©
      simpa using mâŸ©
  left_inv x := by simp
  right_inv y := by simp
#align equiv.image Equiv.image
#align equiv.image_symm_apply_coe Equiv.image_symm_apply_coe
#align equiv.image_apply_coe Equiv.image_apply_coe

namespace Set

-- Porting note: Removed attribute @[simps apply symm_apply]
/-- `univ Î±` is equivalent to `Î±`. -/
protected def univ (Î±) : @univ Î± â‰ƒ Î± :=
  âŸ¨Subtype.val, fun a => âŸ¨a, trivialâŸ©, fun âŸ¨_, _âŸ© => rfl, fun _ => rflâŸ©
#align equiv.set.univ Equiv.Set.univ

/-- An empty set is equivalent to the `Empty` type. -/
protected def empty (Î±) : (âˆ… : Set Î±) â‰ƒ Empty :=
  equivEmpty _
#align equiv.set.empty Equiv.Set.empty

/-- An empty set is equivalent to a `PEmpty` type. -/
protected def pempty (Î±) : (âˆ… : Set Î±) â‰ƒ PEmpty :=
  equivPEmpty _
#align equiv.set.pempty Equiv.Set.pempty

/-- If sets `s` and `t` are separated by a decidable predicate, then `s âˆª t` is equivalent to
`s âŠ• t`. -/
protected def union' {Î±} {s t : Set Î±} (p : Î± â†’ Prop) [DecidablePred p] (hs : âˆ€ x âˆˆ s, p x)
    (ht : âˆ€ x âˆˆ t, Â¬p x) : (s âˆª t : Set Î±) â‰ƒ s âŠ• t where
  toFun x :=
    if hp : p x then Sum.inl âŸ¨_, x.2.resolve_right fun xt => ht _ xt hpâŸ©
    else Sum.inr âŸ¨_, x.2.resolve_left fun xs => hp (hs _ xs)âŸ©
  invFun o :=
    match o with
    | Sum.inl x => âŸ¨x, Or.inl x.2âŸ©
    | Sum.inr x => âŸ¨x, Or.inr x.2âŸ©
  left_inv := fun âŸ¨x, h'âŸ© => by by_cases h : p x <;> simp [h]
  right_inv o := by
    rcases o with (âŸ¨x, hâŸ© | âŸ¨x, hâŸ©) <;> [simp [hs _ h]; simp [ht _ h]]
#align equiv.set.union' Equiv.Set.union'

/-- If sets `s` and `t` are disjoint, then `s âˆª t` is equivalent to `s âŠ• t`. -/
protected def union {Î±} {s t : Set Î±} [DecidablePred fun x => x âˆˆ s] (H : s âˆ© t âŠ† âˆ…) :
    (s âˆª t : Set Î±) â‰ƒ s âŠ• t :=
  Set.union' (fun x => x âˆˆ s) (fun _ => id) fun _ xt xs => H âŸ¨xs, xtâŸ©
#align equiv.set.union Equiv.Set.union

theorem union_apply_left {Î±} {s t : Set Î±} [DecidablePred fun x => x âˆˆ s] (H : s âˆ© t âŠ† âˆ…)
    {a : (s âˆª t : Set Î±)} (ha : â†‘a âˆˆ s) : Equiv.Set.union H a = Sum.inl âŸ¨a, haâŸ© :=
  dif_pos ha
#align equiv.set.union_apply_left Equiv.Set.union_apply_left

theorem union_apply_right {Î±} {s t : Set Î±} [DecidablePred fun x => x âˆˆ s] (H : s âˆ© t âŠ† âˆ…)
    {a : (s âˆª t : Set Î±)} (ha : â†‘a âˆˆ t) : Equiv.Set.union H a = Sum.inr âŸ¨a, haâŸ© :=
  dif_neg fun h => H âŸ¨h, haâŸ©
#align equiv.set.union_apply_right Equiv.Set.union_apply_right

@[simp]
theorem union_symm_apply_left {Î±} {s t : Set Î±} [DecidablePred fun x => x âˆˆ s] (H : s âˆ© t âŠ† âˆ…)
    (a : s) : (Equiv.Set.union H).symm (Sum.inl a) = âŸ¨a, by simpâŸ© :=
  rfl
#align equiv.set.union_symm_apply_left Equiv.Set.union_symm_apply_left

@[simp]
theorem union_symm_apply_right {Î±} {s t : Set Î±} [DecidablePred fun x => x âˆˆ s] (H : s âˆ© t âŠ† âˆ…)
    (a : t) : (Equiv.Set.union H).symm (Sum.inr a) = âŸ¨a, by simpâŸ© :=
  rfl
#align equiv.set.union_symm_apply_right Equiv.Set.union_symm_apply_right

/-- A singleton set is equivalent to a `PUnit` type. -/
protected def singleton {Î±} (a : Î±) : ({a} : Set Î±) â‰ƒ PUnit.{u} :=
  âŸ¨fun _ => PUnit.unit, fun _ => âŸ¨a, mem_singleton _âŸ©, fun âŸ¨x, hâŸ© => by
    simp? at h says simp only [mem_singleton_iff] at h
    subst x
    rfl, fun âŸ¨âŸ© => rflâŸ©
#align equiv.set.singleton Equiv.Set.singleton

/-- Equal sets are equivalent.

TODO: this is the same as `Equiv.setCongr`! -/
@[simps! apply symm_apply]
protected def ofEq {Î± : Type u} {s t : Set Î±} (h : s = t) : s â‰ƒ t :=
  Equiv.setCongr h
#align equiv.set.of_eq Equiv.Set.ofEq

/-- If `a âˆ‰ s`, then `insert a s` is equivalent to `s âŠ• PUnit`. -/
protected def insert {Î±} {s : Set.{u} Î±} [DecidablePred (Â· âˆˆ s)] {a : Î±} (H : a âˆ‰ s) :
    (insert a s : Set Î±) â‰ƒ Sum s PUnit.{u + 1} :=
  calc
    (insert a s : Set Î±) â‰ƒ â†¥(s âˆª {a}) := Equiv.Set.ofEq (by simp)
    _ â‰ƒ Sum s ({a} : Set Î±) := Equiv.Set.union fun x âŸ¨hx, _âŸ© => by simp_all
    _ â‰ƒ Sum s PUnit.{u + 1} := sumCongr (Equiv.refl _) (Equiv.Set.singleton _)
#align equiv.set.insert Equiv.Set.insert

@[simp]
theorem insert_symm_apply_inl {Î±} {s : Set.{u} Î±} [DecidablePred (Â· âˆˆ s)] {a : Î±} (H : a âˆ‰ s)
    (b : s) : (Equiv.Set.insert H).symm (Sum.inl b) = âŸ¨b, Or.inr b.2âŸ© :=
  rfl
#align equiv.set.insert_symm_apply_inl Equiv.Set.insert_symm_apply_inl

@[simp]
theorem insert_symm_apply_inr {Î±} {s : Set.{u} Î±} [DecidablePred (Â· âˆˆ s)] {a : Î±} (H : a âˆ‰ s)
    (b : PUnit.{u + 1}) : (Equiv.Set.insert H).symm (Sum.inr b) = âŸ¨a, Or.inl rflâŸ© :=
  rfl
#align equiv.set.insert_symm_apply_inr Equiv.Set.insert_symm_apply_inr

@[simp]
theorem insert_apply_left {Î±} {s : Set.{u} Î±} [DecidablePred (Â· âˆˆ s)] {a : Î±} (H : a âˆ‰ s) :
    Equiv.Set.insert H âŸ¨a, Or.inl rflâŸ© = Sum.inr PUnit.unit :=
  (Equiv.Set.insert H).apply_eq_iff_eq_symm_apply.2 rfl
#align equiv.set.insert_apply_left Equiv.Set.insert_apply_left

@[simp]
theorem insert_apply_right {Î±} {s : Set.{u} Î±} [DecidablePred (Â· âˆˆ s)] {a : Î±} (H : a âˆ‰ s) (b : s) :
    Equiv.Set.insert H âŸ¨b, Or.inr b.2âŸ© = Sum.inl b :=
  (Equiv.Set.insert H).apply_eq_iff_eq_symm_apply.2 rfl
#align equiv.set.insert_apply_right Equiv.Set.insert_apply_right

/-- If `s : Set Î±` is a set with decidable membership, then `s âŠ• sá¶œ` is equivalent to `Î±`. -/
protected def sumCompl {Î±} (s : Set Î±) [DecidablePred (Â· âˆˆ s)] : Sum s (sá¶œ : Set Î±) â‰ƒ Î± :=
  calc
    Sum s (sá¶œ : Set Î±) â‰ƒ â†¥(s âˆª sá¶œ) := (Equiv.Set.union (by simp [Set.ext_iff])).symm
    _ â‰ƒ @univ Î± := Equiv.Set.ofEq (by simp)
    _ â‰ƒ Î± := Equiv.Set.univ _
#align equiv.set.sum_compl Equiv.Set.sumCompl

@[simp]
theorem sumCompl_apply_inl {Î± : Type u} (s : Set Î±) [DecidablePred (Â· âˆˆ s)] (x : s) :
    Equiv.Set.sumCompl s (Sum.inl x) = x :=
  rfl
#align equiv.set.sum_compl_apply_inl Equiv.Set.sumCompl_apply_inl

@[simp]
theorem sumCompl_apply_inr {Î± : Type u} (s : Set Î±) [DecidablePred (Â· âˆˆ s)] (x : (sá¶œ : Set Î±)) :
    Equiv.Set.sumCompl s (Sum.inr x) = x :=
  rfl
#align equiv.set.sum_compl_apply_inr Equiv.Set.sumCompl_apply_inr

theorem sumCompl_symm_apply_of_mem {Î± : Type u} {s : Set Î±} [DecidablePred (Â· âˆˆ s)] {x : Î±}
    (hx : x âˆˆ s) : (Equiv.Set.sumCompl s).symm x = Sum.inl âŸ¨x, hxâŸ© := by
  have : ((âŸ¨x, Or.inl hxâŸ© : (s âˆª sá¶œ : Set Î±)) : Î±) âˆˆ s := hx
  rw [Equiv.Set.sumCompl]
  simpa using Set.union_apply_left (by simp) this
#align equiv.set.sum_compl_symm_apply_of_mem Equiv.Set.sumCompl_symm_apply_of_mem

theorem sumCompl_symm_apply_of_not_mem {Î± : Type u} {s : Set Î±} [DecidablePred (Â· âˆˆ s)] {x : Î±}
    (hx : x âˆ‰ s) : (Equiv.Set.sumCompl s).symm x = Sum.inr âŸ¨x, hxâŸ© := by
  have : ((âŸ¨x, Or.inr hxâŸ© : (s âˆª sá¶œ : Set Î±)) : Î±) âˆˆ sá¶œ := hx
  rw [Equiv.Set.sumCompl]
  simpa using Set.union_apply_right (by simp) this
#align equiv.set.sum_compl_symm_apply_of_not_mem Equiv.Set.sumCompl_symm_apply_of_not_mem

@[simp]
theorem sumCompl_symm_apply {Î± : Type*} {s : Set Î±} [DecidablePred (Â· âˆˆ s)] {x : s} :
    (Equiv.Set.sumCompl s).symm x = Sum.inl x := by
  cases' x with x hx; exact Set.sumCompl_symm_apply_of_mem hx
#align equiv.set.sum_compl_symm_apply Equiv.Set.sumCompl_symm_apply

@[simp]
theorem sumCompl_symm_apply_compl {Î± : Type*} {s : Set Î±} [DecidablePred (Â· âˆˆ s)]
    {x : (sá¶œ : Set Î±)} : (Equiv.Set.sumCompl s).symm x = Sum.inr x := by
  cases' x with x hx; exact Set.sumCompl_symm_apply_of_not_mem hx
#align equiv.set.sum_compl_symm_apply_compl Equiv.Set.sumCompl_symm_apply_compl

/-- `sumDiffSubset s t` is the natural equivalence between
`s âŠ• (t \ s)` and `t`, where `s` and `t` are two sets. -/
protected def sumDiffSubset {Î±} {s t : Set Î±} (h : s âŠ† t) [DecidablePred (Â· âˆˆ s)] :
    Sum s (t \ s : Set Î±) â‰ƒ t :=
  calc
    Sum s (t \ s : Set Î±) â‰ƒ (s âˆª t \ s : Set Î±) :=
      (Equiv.Set.union (by simp [inter_diff_self])).symm
    _ â‰ƒ t := Equiv.Set.ofEq (by simp [union_diff_self, union_eq_self_of_subset_left h])
#align equiv.set.sum_diff_subset Equiv.Set.sumDiffSubset

@[simp]
theorem sumDiffSubset_apply_inl {Î±} {s t : Set Î±} (h : s âŠ† t) [DecidablePred (Â· âˆˆ s)] (x : s) :
    Equiv.Set.sumDiffSubset h (Sum.inl x) = inclusion h x :=
  rfl
#align equiv.set.sum_diff_subset_apply_inl Equiv.Set.sumDiffSubset_apply_inl

@[simp]
theorem sumDiffSubset_apply_inr {Î±} {s t : Set Î±} (h : s âŠ† t) [DecidablePred (Â· âˆˆ s)]
    (x : (t \ s : Set Î±)) : Equiv.Set.sumDiffSubset h (Sum.inr x) = inclusion diff_subset x :=
  rfl
#align equiv.set.sum_diff_subset_apply_inr Equiv.Set.sumDiffSubset_apply_inr

theorem sumDiffSubset_symm_apply_of_mem {Î±} {s t : Set Î±} (h : s âŠ† t) [DecidablePred (Â· âˆˆ s)]
    {x : t} (hx : x.1 âˆˆ s) : (Equiv.Set.sumDiffSubset h).symm x = Sum.inl âŸ¨x, hxâŸ© := by
  apply (Equiv.Set.sumDiffSubset h).injective
  simp only [apply_symm_apply, sumDiffSubset_apply_inl]
  exact Subtype.eq rfl
#align equiv.set.sum_diff_subset_symm_apply_of_mem Equiv.Set.sumDiffSubset_symm_apply_of_mem

theorem sumDiffSubset_symm_apply_of_not_mem {Î±} {s t : Set Î±} (h : s âŠ† t) [DecidablePred (Â· âˆˆ s)]
    {x : t} (hx : x.1 âˆ‰ s) : (Equiv.Set.sumDiffSubset h).symm x = Sum.inr âŸ¨x, âŸ¨x.2, hxâŸ©âŸ© := by
  apply (Equiv.Set.sumDiffSubset h).injective
  simp only [apply_symm_apply, sumDiffSubset_apply_inr]
  exact Subtype.eq rfl
#align equiv.set.sum_diff_subset_symm_apply_of_not_mem Equiv.Set.sumDiffSubset_symm_apply_of_not_mem

/-- If `s` is a set with decidable membership, then the sum of `s âˆª t` and `s âˆ© t` is equivalent
to `s âŠ• t`. -/
protected def unionSumInter {Î± : Type u} (s t : Set Î±) [DecidablePred (Â· âˆˆ s)] :
    Sum (s âˆª t : Set Î±) (s âˆ© t : Set Î±) â‰ƒ Sum s t :=
  calc
    Sum (s âˆª t : Set Î±) (s âˆ© t : Set Î±)
      â‰ƒ Sum (s âˆª t \ s : Set Î±) (s âˆ© t : Set Î±) := by rw [union_diff_self]
    _ â‰ƒ Sum (Sum s (t \ s : Set Î±)) (s âˆ© t : Set Î±) :=
      sumCongr (Set.union <| subset_empty_iff.2 (inter_diff_self _ _)) (Equiv.refl _)
    _ â‰ƒ Sum s (Sum (t \ s : Set Î±) (s âˆ© t : Set Î±)) := sumAssoc _ _ _
    _ â‰ƒ Sum s (t \ s âˆª s âˆ© t : Set Î±) :=
      sumCongr (Equiv.refl _)
        (by
          refine (Set.union' (Â· âˆ‰ s) ?_ ?_).symm
          exacts [fun x hx => hx.2, fun x hx => not_not_intro hx.1])
    _ â‰ƒ Sum s t := by
      { rw [(_ : t \ s âˆª s âˆ© t = t)]
        rw [union_comm, inter_comm, inter_union_diff] }
#align equiv.set.union_sum_inter Equiv.Set.unionSumInter

/-- Given an equivalence `eâ‚€` between sets `s : Set Î±` and `t : Set Î²`, the set of equivalences
`e : Î± â‰ƒ Î²` such that `e â†‘x = â†‘(eâ‚€ x)` for each `x : s` is equivalent to the set of equivalences
between `sá¶œ` and `tá¶œ`. -/
protected def compl {Î± : Type u} {Î² : Type v} {s : Set Î±} {t : Set Î²} [DecidablePred (Â· âˆˆ s)]
    [DecidablePred (Â· âˆˆ t)] (eâ‚€ : s â‰ƒ t) :
    { e : Î± â‰ƒ Î² // âˆ€ x : s, e x = eâ‚€ x } â‰ƒ ((sá¶œ : Set Î±) â‰ƒ (tá¶œ : Set Î²)) where
  toFun e :=
    subtypeEquiv e fun a =>
      not_congr <|
        Iff.symm <|
          MapsTo.mem_iff (mapsTo_iff_exists_map_subtype.2 âŸ¨eâ‚€, e.2âŸ©)
            (SurjOn.mapsTo_compl
              (surjOn_iff_exists_map_subtype.2 âŸ¨t, eâ‚€, Subset.refl t, eâ‚€.surjective, e.2âŸ©)
              e.1.injective)
  invFun eâ‚ :=
    Subtype.mk
      (calc
        Î± â‰ƒ Sum s (sá¶œ : Set Î±) := (Set.sumCompl s).symm
        _ â‰ƒ Sum t (tá¶œ : Set Î²) := eâ‚€.sumCongr eâ‚
        _ â‰ƒ Î² := Set.sumCompl t
        )
      fun x => by
      simp only [Sum.map_inl, trans_apply, sumCongr_apply, Set.sumCompl_apply_inl,
        Set.sumCompl_symm_apply, Trans.trans]
  left_inv e := by
    ext x
    by_cases hx : x âˆˆ s
    Â· simp only [Set.sumCompl_symm_apply_of_mem hx, â† e.prop âŸ¨x, hxâŸ©, Sum.map_inl, sumCongr_apply,
        trans_apply, Subtype.coe_mk, Set.sumCompl_apply_inl, Trans.trans]
    Â· simp only [Set.sumCompl_symm_apply_of_not_mem hx, Sum.map_inr, subtypeEquiv_apply,
        Set.sumCompl_apply_inr, trans_apply, sumCongr_apply, Subtype.coe_mk, Trans.trans]
  right_inv e :=
    Equiv.ext fun x => by
      simp only [Sum.map_inr, subtypeEquiv_apply, Set.sumCompl_apply_inr, Function.comp_apply,
        sumCongr_apply, Equiv.coe_trans, Subtype.coe_eta, Subtype.coe_mk, Trans.trans,
        Set.sumCompl_symm_apply_compl]
#align equiv.set.compl Equiv.Set.compl

/-- The set product of two sets is equivalent to the type product of their coercions to types. -/
protected def prod {Î± Î²} (s : Set Î±) (t : Set Î²) : â†¥(s Ã—Ë¢ t) â‰ƒ s Ã— t :=
  @subtypeProdEquivProd Î± Î² s t
#align equiv.set.prod Equiv.Set.prod

/-- The set `Set.pi Set.univ s` is equivalent to `Î  a, s a`. -/
@[simps]
protected def univPi {Î± : Type*} {Î² : Î± â†’ Type*} (s : âˆ€ a, Set (Î² a)) :
    pi univ s â‰ƒ âˆ€ a, s a where
  toFun f a := âŸ¨(f : âˆ€ a, Î² a) a, f.2 a (mem_univ a)âŸ©
  invFun f := âŸ¨fun a => f a, fun a _ => (f a).2âŸ©
  left_inv := fun âŸ¨f, hfâŸ© => by
    ext a
    rfl
  right_inv f := by
    ext a
    rfl
#align equiv.set.univ_pi Equiv.Set.univPi
#align equiv.set.univ_pi_symm_apply_coe Equiv.Set.univPi_symm_apply_coe
#align equiv.set.univ_pi_apply_coe Equiv.Set.univPi_apply_coe

/-- If a function `f` is injective on a set `s`, then `s` is equivalent to `f '' s`. -/
protected noncomputable def imageOfInjOn {Î± Î²} (f : Î± â†’ Î²) (s : Set Î±) (H : InjOn f s) :
    s â‰ƒ f '' s :=
  âŸ¨fun p => âŸ¨f p, mem_image_of_mem f p.2âŸ©, fun p =>
    âŸ¨Classical.choose p.2, (Classical.choose_spec p.2).1âŸ©, fun âŸ¨_, hâŸ© =>
    Subtype.eq
      (H (Classical.choose_spec (mem_image_of_mem f h)).1 h
        (Classical.choose_spec (mem_image_of_mem f h)).2),
    fun âŸ¨_, hâŸ© => Subtype.eq (Classical.choose_spec h).2âŸ©
#align equiv.set.image_of_inj_on Equiv.Set.imageOfInjOn

/-- If `f` is an injective function, then `s` is equivalent to `f '' s`. -/
@[simps! apply]
protected noncomputable def image {Î± Î²} (f : Î± â†’ Î²) (s : Set Î±) (H : Injective f) : s â‰ƒ f '' s :=
  Equiv.Set.imageOfInjOn f s H.injOn
#align equiv.set.image Equiv.Set.image
#align equiv.set.image_apply Equiv.Set.image_apply

@[simp]
protected theorem image_symm_apply {Î± Î²} (f : Î± â†’ Î²) (s : Set Î±) (H : Injective f) (x : Î±)
    (h : f x âˆˆ f '' s) : (Set.image f s H).symm âŸ¨f x, hâŸ© = âŸ¨x, H.mem_set_image.1 hâŸ© :=
  (Equiv.symm_apply_eq _).2 rfl
#align equiv.set.image_symm_apply Equiv.Set.image_symm_apply

theorem image_symm_preimage {Î± Î²} {f : Î± â†’ Î²} (hf : Injective f) (u s : Set Î±) :
    (fun x => (Set.image f s hf).symm x : f '' s â†’ Î±) â»Â¹' u = Subtype.val â»Â¹' (f '' u) := by
  ext âŸ¨b, a, has, rflâŸ©
  simp [hf.eq_iff]
#align equiv.set.image_symm_preimage Equiv.Set.image_symm_preimage

/-- If `Î±` is equivalent to `Î²`, then `Set Î±` is equivalent to `Set Î²`. -/
@[simps]
protected def congr {Î± Î² : Type*} (e : Î± â‰ƒ Î²) : Set Î± â‰ƒ Set Î² :=
  âŸ¨fun s => e '' s, fun t => e.symm '' t, symm_image_image e, symm_image_image e.symmâŸ©
#align equiv.set.congr Equiv.Set.congr
#align equiv.set.congr_apply Equiv.Set.congr_apply
#align equiv.set.congr_symm_apply Equiv.Set.congr_symm_apply

/-- The set `{x âˆˆ s | t x}` is equivalent to the set of `x : s` such that `t x`. -/
protected def sep {Î± : Type u} (s : Set Î±) (t : Î± â†’ Prop) :
    ({ x âˆˆ s | t x } : Set Î±) â‰ƒ { x : s | t x } :=
  (Equiv.subtypeSubtypeEquivSubtypeInter s t).symm
#align equiv.set.sep Equiv.Set.sep

/-- The set `ğ’« S := {x | x âŠ† S}` is equivalent to the type `Set S`. -/
protected def powerset {Î±} (S : Set Î±) :
    ğ’« S â‰ƒ Set S where
  toFun := fun x : ğ’« S => Subtype.val â»Â¹' (x : Set Î±)
  invFun := fun x : Set S => âŸ¨Subtype.val '' x, by rintro _ âŸ¨a : S, _, rflâŸ©; exact a.2âŸ©
  left_inv x := by ext y;exact âŸ¨fun âŸ¨âŸ¨_, _âŸ©, h, rflâŸ© => h, fun h => âŸ¨âŸ¨_, x.2 hâŸ©, h, rflâŸ©âŸ©
  right_inv x := by ext; simp
#align equiv.set.powerset Equiv.Set.powerset

/-- If `s` is a set in `range f`,
then its image under `rangeSplitting f` is in bijection (via `f`) with `s`.
-/
@[simps]
noncomputable def rangeSplittingImageEquiv {Î± Î² : Type*} (f : Î± â†’ Î²) (s : Set (range f)) :
    rangeSplitting f '' s â‰ƒ s where
  toFun x :=
    âŸ¨âŸ¨f x, by simpâŸ©, by
      rcases x with âŸ¨x, âŸ¨y, âŸ¨m, rflâŸ©âŸ©âŸ©
      simpa [apply_rangeSplitting f] using mâŸ©
  invFun x := âŸ¨rangeSplitting f x, âŸ¨x, âŸ¨x.2, rflâŸ©âŸ©âŸ©
  left_inv x := by
    rcases x with âŸ¨x, âŸ¨y, âŸ¨m, rflâŸ©âŸ©âŸ©
    simp [apply_rangeSplitting f]
  right_inv x := by simp [apply_rangeSplitting f]
#align equiv.set.range_splitting_image_equiv Equiv.Set.rangeSplittingImageEquiv
#align equiv.set.range_splitting_image_equiv_symm_apply_coe Equiv.Set.rangeSplittingImageEquiv_symm_apply_coe
#align equiv.set.range_splitting_image_equiv_apply_coe_coe Equiv.Set.rangeSplittingImageEquiv_apply_coe_coe

/-- Equivalence between the range of `Sum.inl : Î± â†’ Î± âŠ• Î²` and `Î±`. -/
@[simps symm_apply_coe]
def rangeInl (Î± Î² : Type*) : Set.range (Sum.inl : Î± â†’ Î± âŠ• Î²) â‰ƒ Î± where
  toFun
  | âŸ¨.inl x, _âŸ© => x
  | âŸ¨.inr _, hâŸ© => False.elim <| by rcases h with âŸ¨x, h'âŸ©; cases h'
  invFun x := âŸ¨.inl x, mem_range_self _âŸ©
  left_inv := fun âŸ¨_, _, rflâŸ© => rfl
  right_inv x := rfl

@[simp] lemma rangeInl_apply_inl {Î± : Type*} (Î² : Type*) (x : Î±) :
    (rangeInl Î± Î²) âŸ¨.inl x, mem_range_self _âŸ© = x :=
  rfl

/-- Equivalence between the range of `Sum.inr : Î² â†’ Î± âŠ• Î²` and `Î²`. -/
@[simps symm_apply_coe]
def rangeInr (Î± Î² : Type*) : Set.range (Sum.inr : Î² â†’ Î± âŠ• Î²) â‰ƒ Î² where
  toFun
  | âŸ¨.inl _, hâŸ© => False.elim <| by rcases h with âŸ¨x, h'âŸ©; cases h'
  | âŸ¨.inr x, _âŸ© => x
  invFun x := âŸ¨.inr x, mem_range_self _âŸ©
  left_inv := fun âŸ¨_, _, rflâŸ© => rfl
  right_inv x := rfl

@[simp] lemma rangeInr_apply_inr (Î± : Type*) {Î² : Type*} (x : Î²) :
    (rangeInr Î± Î²) âŸ¨.inr x, mem_range_self _âŸ© = x :=
  rfl

end Set

/-- If `f : Î± â†’ Î²` has a left-inverse when `Î±` is nonempty, then `Î±` is computably equivalent to the
range of `f`.

While awkward, the `Nonempty Î±` hypothesis on `f_inv` and `hf` allows this to be used when `Î±` is
empty too. This hypothesis is absent on analogous definitions on stronger `Equiv`s like
`LinearEquiv.ofLeftInverse` and `RingEquiv.ofLeftInverse` as their typeclass assumptions
are already sufficient to ensure non-emptiness. -/
@[simps]
def ofLeftInverse {Î± Î² : Sort _} (f : Î± â†’ Î²) (f_inv : Nonempty Î± â†’ Î² â†’ Î±)
    (hf : âˆ€ h : Nonempty Î±, LeftInverse (f_inv h) f) :
    Î± â‰ƒ range f where
  toFun a := âŸ¨f a, a, rflâŸ©
  invFun b := f_inv (nonempty_of_exists b.2) b
  left_inv a := hf âŸ¨aâŸ© a
  right_inv := fun âŸ¨b, a, haâŸ© =>
    Subtype.eq <| show f (f_inv âŸ¨aâŸ© b) = b from Eq.trans (congr_arg f <| ha â–¸ hf _ a) ha
#align equiv.of_left_inverse Equiv.ofLeftInverse
#align equiv.of_left_inverse_apply_coe Equiv.ofLeftInverse_apply_coe
#align equiv.of_left_inverse_symm_apply Equiv.ofLeftInverse_symm_apply

/-- If `f : Î± â†’ Î²` has a left-inverse, then `Î±` is computably equivalent to the range of `f`.

Note that if `Î±` is empty, no such `f_inv` exists and so this definition can't be used, unlike
the stronger but less convenient `ofLeftInverse`. -/
abbrev ofLeftInverse' {Î± Î² : Sort _} (f : Î± â†’ Î²) (f_inv : Î² â†’ Î±) (hf : LeftInverse f_inv f) :
    Î± â‰ƒ range f :=
  ofLeftInverse f (fun _ => f_inv) fun _ => hf
#align equiv.of_left_inverse' Equiv.ofLeftInverse'

/-- If `f : Î± â†’ Î²` is an injective function, then domain `Î±` is equivalent to the range of `f`. -/
@[simps! apply]
noncomputable def ofInjective {Î± Î²} (f : Î± â†’ Î²) (hf : Injective f) : Î± â‰ƒ range f :=
  Equiv.ofLeftInverse f (fun _ => Function.invFun f) fun _ => Function.leftInverse_invFun hf
#align equiv.of_injective Equiv.ofInjective
#align equiv.of_injective_apply Equiv.ofInjective_apply

theorem apply_ofInjective_symm {Î± Î²} {f : Î± â†’ Î²} (hf : Injective f) (b : range f) :
    f ((ofInjective f hf).symm b) = b :=
  Subtype.ext_iff.1 <| (ofInjective f hf).apply_symm_apply b
#align equiv.apply_of_injective_symm Equiv.apply_ofInjective_symm

@[simp]
theorem ofInjective_symm_apply {Î± Î²} {f : Î± â†’ Î²} (hf : Injective f) (a : Î±) :
    (ofInjective f hf).symm âŸ¨f a, âŸ¨a, rflâŸ©âŸ© = a := by
  apply (ofInjective f hf).injective
  simp [apply_ofInjective_symm hf]
#align equiv.of_injective_symm_apply Equiv.ofInjective_symm_apply

theorem coe_ofInjective_symm {Î± Î²} {f : Î± â†’ Î²} (hf : Injective f) :
    ((ofInjective f hf).symm : range f â†’ Î±) = rangeSplitting f := by
  ext âŸ¨y, x, rflâŸ©
  apply hf
  simp [apply_rangeSplitting f]
#align equiv.coe_of_injective_symm Equiv.coe_ofInjective_symm

@[simp]
theorem self_comp_ofInjective_symm {Î± Î²} {f : Î± â†’ Î²} (hf : Injective f) :
    f âˆ˜ (ofInjective f hf).symm = Subtype.val :=
  funext fun x => apply_ofInjective_symm hf x
#align equiv.self_comp_of_injective_symm Equiv.self_comp_ofInjective_symm

theorem ofLeftInverse_eq_ofInjective {Î± Î² : Type*} (f : Î± â†’ Î²) (f_inv : Nonempty Î± â†’ Î² â†’ Î±)
    (hf : âˆ€ h : Nonempty Î±, LeftInverse (f_inv h) f) :
    ofLeftInverse f f_inv hf =
      ofInjective f ((isEmpty_or_nonempty Î±).elim (fun h _ _ _ => Subsingleton.elim _ _)
        (fun h => (hf h).injective)) := by
  ext
  simp
#align equiv.of_left_inverse_eq_of_injective Equiv.ofLeftInverse_eq_ofInjective

theorem ofLeftInverse'_eq_ofInjective {Î± Î² : Type*} (f : Î± â†’ Î²) (f_inv : Î² â†’ Î±)
    (hf : LeftInverse f_inv f) : ofLeftInverse' f f_inv hf = ofInjective f hf.injective := by
  ext
  simp
#align equiv.of_left_inverse'_eq_of_injective Equiv.ofLeftInverse'_eq_ofInjective

protected theorem set_forall_iff {Î± Î²} (e : Î± â‰ƒ Î²) {p : Set Î± â†’ Prop} :
    (âˆ€ a, p a) â†” âˆ€ a, p (e â»Â¹' a) :=
  e.injective.preimage_surjective.forall
#align equiv.set_forall_iff Equiv.set_forall_iff

theorem preimage_piEquivPiSubtypeProd_symm_pi {Î± : Type*} {Î² : Î± â†’ Type*} (p : Î± â†’ Prop)
    [DecidablePred p] (s : âˆ€ i, Set (Î² i)) :
    (piEquivPiSubtypeProd p Î²).symm â»Â¹' pi univ s =
      (pi univ fun i : { i // p i } => s i) Ã—Ë¢ pi univ fun i : { i // Â¬p i } => s i := by
  ext âŸ¨f, gâŸ©
  simp only [mem_preimage, mem_univ_pi, prod_mk_mem_set_prod_eq, Subtype.forall, â† forall_and]
  refine forall_congr' fun i => ?_
  dsimp only [Subtype.coe_mk]
  by_cases hi : p i <;> simp [hi]
#align equiv.preimage_pi_equiv_pi_subtype_prod_symm_pi Equiv.preimage_piEquivPiSubtypeProd_symm_pi

-- See also `Equiv.sigmaFiberEquiv`.
/-- `sigmaPreimageEquiv f` for `f : Î± â†’ Î²` is the natural equivalence between
the type of all preimages of points under `f` and the total space `Î±`. -/
@[simps!]
def sigmaPreimageEquiv {Î± Î²} (f : Î± â†’ Î²) : (Î£b, f â»Â¹' {b}) â‰ƒ Î± :=
  sigmaFiberEquiv f
#align equiv.sigma_preimage_equiv Equiv.sigmaPreimageEquiv
#align equiv.sigma_preimage_equiv_symm_apply_snd_coe Equiv.sigmaPreimageEquiv_symm_apply_snd_coe
#align equiv.sigma_preimage_equiv_apply Equiv.sigmaPreimageEquiv_apply
#align equiv.sigma_preimage_equiv_symm_apply_fst Equiv.sigmaPreimageEquiv_symm_apply_fst

-- See also `Equiv.ofFiberEquiv`.
/-- A family of equivalences between preimages of points gives an equivalence between domains. -/
@[simps!]
def ofPreimageEquiv {Î± Î² Î³} {f : Î± â†’ Î³} {g : Î² â†’ Î³} (e : âˆ€ c, f â»Â¹' {c} â‰ƒ g â»Â¹' {c}) : Î± â‰ƒ Î² :=
  Equiv.ofFiberEquiv e
#align equiv.of_preimage_equiv Equiv.ofPreimageEquiv
#align equiv.of_preimage_equiv_apply Equiv.ofPreimageEquiv_apply
#align equiv.of_preimage_equiv_symm_apply Equiv.ofPreimageEquiv_symm_apply

theorem ofPreimageEquiv_map {Î± Î² Î³} {f : Î± â†’ Î³} {g : Î² â†’ Î³} (e : âˆ€ c, f â»Â¹' {c} â‰ƒ g â»Â¹' {c})
    (a : Î±) : g (ofPreimageEquiv e a) = f a :=
  Equiv.ofFiberEquiv_map e a
#align equiv.of_preimage_equiv_map Equiv.ofPreimageEquiv_map

end Equiv

/-- If a function is a bijection between two sets `s` and `t`, then it induces an
equivalence between the types `â†¥s` and `â†¥t`. -/
noncomputable def Set.BijOn.equiv {Î± : Type*} {Î² : Type*} {s : Set Î±} {t : Set Î²} (f : Î± â†’ Î²)
    (h : BijOn f s t) : s â‰ƒ t :=
  Equiv.ofBijective _ h.bijective
#align set.bij_on.equiv Set.BijOn.equiv

/-- The composition of an updated function with an equiv on a subtype can be expressed as an
updated function. -/
-- Porting note: replace `s : Set Î±` and `: s` with `p : Î± â†’ Prop` and `: Subtype p`, since the
-- former now unfolds syntactically to a less general case of the latter.
theorem dite_comp_equiv_update {Î± : Type*} {Î² : Sort*} {Î³ : Sort*} {p : Î± â†’ Prop}
    (e : Î² â‰ƒ Subtype p)
    (v : Î² â†’ Î³) (w : Î± â†’ Î³) (j : Î²) (x : Î³) [DecidableEq Î²] [DecidableEq Î±]
    [âˆ€ j, Decidable (p j)] :
    (fun i : Î± => if h : p i then (Function.update v j x) (e.symm âŸ¨i, hâŸ©) else w i) =
      Function.update (fun i : Î± => if h : p i then v (e.symm âŸ¨i, hâŸ©) else w i) (e j) x := by
  ext i
  by_cases h : p i
  Â· rw [dif_pos h, Function.update_apply_equiv_apply, Equiv.symm_symm,
      Function.update_apply, Function.update_apply, dif_pos h]
    have h_coe : (âŸ¨i, hâŸ© : Subtype p) = e j â†” i = e j :=
      Subtype.ext_iff.trans (by rw [Subtype.coe_mk])
    simp [h_coe]
  Â· have : i â‰  e j := by
      contrapose! h
      have : p (e j : Î±) := (e j).2
      rwa [â† h] at this
    simp [h, this]
#align dite_comp_equiv_update dite_comp_equiv_updateâ‚“

section Swap

variable {Î± : Type*} [DecidableEq Î±] {a b : Î±} {s : Set Î±}

theorem Equiv.swap_bijOn_self (hs : a âˆˆ s â†” b âˆˆ s) : BijOn (Equiv.swap a b) s s := by
  refine âŸ¨fun x hx â†¦ ?_, (Equiv.injective _).injOn, fun x hx â†¦ ?_âŸ©
  Â· obtain (rfl | hxa) := eq_or_ne x a; rwa [swap_apply_left, â† hs]
    obtain (rfl | hxb) := eq_or_ne x b; rwa [swap_apply_right, hs]
    rwa [swap_apply_of_ne_of_ne hxa hxb]
  obtain (rfl | hxa) := eq_or_ne x a; simp [hs.1 hx]
  obtain (rfl | hxb) := eq_or_ne x b; simp [hs.2 hx]
  exact âŸ¨x, hx, swap_apply_of_ne_of_ne hxa hxbâŸ©

theorem Equiv.swap_bijOn_exchange (ha : a âˆˆ s) (hb : b âˆ‰ s) :
    BijOn (Equiv.swap a b) s (insert b (s \ {a})) := by
  refine âŸ¨fun x hx â†¦ ?_, (Equiv.injective _).injOn, fun x hx â†¦ ?_âŸ©
  Â· obtain (rfl | hxa) := eq_or_ne x a; simp [swap_apply_left]
    rw [swap_apply_of_ne_of_ne hxa (by rintro rfl; contradiction)]
    exact .inr âŸ¨hx, hxaâŸ©
  obtain (rfl | hxb) := eq_or_ne x b; exact âŸ¨a, ha, by simpâŸ©
  simp only [mem_insert_iff, mem_diff, mem_singleton_iff, or_iff_right hxb] at hx
  exact âŸ¨x, hx.1, swap_apply_of_ne_of_ne hx.2 hxbâŸ©

end Swap
