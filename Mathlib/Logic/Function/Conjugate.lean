/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Logic.Function.Basic
import Mathlib.Tactic.Relation.Rfl

#align_import logic.function.conjugate from "leanprover-community/mathlib"@"c4658a649d216f57e99621708b09dcb3dcccbd23"

/-!
# Semiconjugate and commuting maps

We define the following predicates:

* `Function.Semiconj`: `f : Î± â†’ Î²` semiconjugates `ga : Î± â†’ Î±` to `gb : Î² â†’ Î²` if `f âˆ˜ ga = gb âˆ˜ f`;
* `Function.Semiconjâ‚‚`: `f : Î± â†’ Î²` semiconjugates a binary operation `ga : Î± â†’ Î± â†’ Î±`
  to `gb : Î² â†’ Î² â†’ Î²` if `f (ga x y) = gb (f x) (f y)`;
* `Function.Commute`: `f : Î± â†’ Î±` commutes with `g : Î± â†’ Î±` if `f âˆ˜ g = g âˆ˜ f`,
  or equivalently `Semiconj f g g`.
-/

namespace Function

variable {Î± : Type*} {Î² : Type*} {Î³ : Type*}

/--
We say that `f : Î± â†’ Î²` semiconjugates `ga : Î± â†’ Î±` to `gb : Î² â†’ Î²` if `f âˆ˜ ga = gb âˆ˜ f`.
We use `âˆ€ x, f (ga x) = gb (f x)` as the definition, so given `h : Function.Semiconj f ga gb` and
`a : Î±`, we have `h a : f (ga a) = gb (f a)` and `h.comp_eq : f âˆ˜ ga = gb âˆ˜ f`.
-/
def Semiconj (f : Î± â†’ Î²) (ga : Î± â†’ Î±) (gb : Î² â†’ Î²) : Prop :=
  âˆ€ x, f (ga x) = gb (f x)
#align function.semiconj Function.Semiconj

namespace Semiconj

variable {f fab : Î± â†’ Î²} {fbc : Î² â†’ Î³} {ga ga' : Î± â†’ Î±} {gb gb' : Î² â†’ Î²} {gc gc' : Î³ â†’ Î³}

protected theorem comp_eq (h : Semiconj f ga gb) : f âˆ˜ ga = gb âˆ˜ f :=
  funext h
#align function.semiconj.comp_eq Function.Semiconj.comp_eq

protected theorem eq (h : Semiconj f ga gb) (x : Î±) : f (ga x) = gb (f x) :=
  h x
#align function.semiconj.eq Function.Semiconj.eq

theorem comp_right (h : Semiconj f ga gb) (h' : Semiconj f ga' gb') :
    Semiconj f (ga âˆ˜ ga') (gb âˆ˜ gb') :=
  fun x â†¦ by simp only [comp_apply, h.eq, h'.eq]
             -- ðŸŽ‰ no goals
#align function.semiconj.comp_right Function.Semiconj.comp_right

theorem comp_left (hab : Semiconj fab ga gb) (hbc : Semiconj fbc gb gc) :
    Semiconj (fbc âˆ˜ fab) ga gc :=
  fun x â†¦ by simp only [comp_apply, hab.eq, hbc.eq]
             -- ðŸŽ‰ no goals
#align function.semiconj.comp_left Function.Semiconj.comp_left

theorem id_right : Semiconj f id id :=
  fun _ â†¦ rfl
#align function.semiconj.id_right Function.Semiconj.id_right

theorem id_left : Semiconj id ga ga :=
  fun _ â†¦ rfl
#align function.semiconj.id_left Function.Semiconj.id_left

theorem inverses_right (h : Semiconj f ga gb) (ha : RightInverse ga' ga) (hb : LeftInverse gb' gb) :
    Semiconj f ga' gb' :=
  fun x â†¦ by rw [â† hb (f (ga' x)), â† h.eq, ha x]
             -- ðŸŽ‰ no goals
#align function.semiconj.inverses_right Function.Semiconj.inverses_right

theorem option_map {f : Î± â†’ Î²} {ga : Î± â†’ Î±} {gb : Î² â†’ Î²} (h : Semiconj f ga gb) :
    Semiconj (Option.map f) (Option.map ga) (Option.map gb)
  | none => rfl
  | some _ => congr_arg some <| h _
#align function.semiconj.option_map Function.Semiconj.option_map

end Semiconj

/--
Two maps `f g : Î± â†’ Î±` commute if `f (g x) = g (f x)` for all `x : Î±`.
Given `h : Function.commute f g` and `a : Î±`, we have `h a : f (g a) = g (f a)` and
`h.comp_eq : f âˆ˜ g = g âˆ˜ f`.
-/
protected def Commute (f g : Î± â†’ Î±) : Prop :=
  Semiconj f g g
#align function.commute Function.Commute

open Function (Commute)

/-- Reinterpret `Function.Semiconj f g g` as `Function.Commute f g`. These two predicates are
definitionally equal but have different dot-notation lemmas. -/
theorem Semiconj.commute {f g : Î± â†’ Î±} (h : Semiconj f g g) : Commute f g := h
#align function.semiconj.commute Function.Semiconj.commute

namespace Commute

variable {f f' g g' : Î± â†’ Î±}

/-- Reinterpret `Function.Commute f g` as `Function.Semiconj f g g`. These two predicates are
definitionally equal but have different dot-notation lemmas. -/
theorem semiconj (h : Commute f g) : Semiconj f g g := h

@[refl]
theorem refl (f : Î± â†’ Î±) : Commute f f :=
  fun _ â†¦ Eq.refl _
#align function.commute.refl Function.Commute.refl

@[symm]
theorem symm (h : Commute f g) : Commute g f :=
  fun x â†¦ (h x).symm
#align function.commute.symm Function.Commute.symm

theorem comp_right (h : Commute f g) (h' : Commute f g') : Commute f (g âˆ˜ g') :=
  Semiconj.comp_right h h'
#align function.commute.comp_right Function.Commute.comp_right

theorem comp_left (h : Commute f g) (h' : Commute f' g) : Commute (f âˆ˜ f') g :=
  (h.symm.comp_right h'.symm).symm
#align function.commute.comp_left Function.Commute.comp_left

theorem id_right : Commute f id :=
  Semiconj.id_right
#align function.commute.id_right Function.Commute.id_right

theorem id_left : Commute id f :=
  Semiconj.id_left
#align function.commute.id_left Function.Commute.id_left

theorem option_map {f g : Î± â†’ Î±} : Commute f g â†’ Commute (Option.map f) (Option.map g) :=
  Semiconj.option_map
#align function.commute.option_map Function.Commute.option_map

end Commute

/--
A map `f` semiconjugates a binary operation `ga` to a binary operation `gb` if
for all `x`, `y` we have `f (ga x y) = gb (f x) (f y)`. E.g., a `MonoidHom`
semiconjugates `(*)` to `(*)`.
-/
def Semiconjâ‚‚ (f : Î± â†’ Î²) (ga : Î± â†’ Î± â†’ Î±) (gb : Î² â†’ Î² â†’ Î²) : Prop :=
  âˆ€ x y, f (ga x y) = gb (f x) (f y)
#align function.semiconjâ‚‚ Function.Semiconjâ‚‚

namespace Semiconjâ‚‚

variable {f : Î± â†’ Î²} {ga : Î± â†’ Î± â†’ Î±} {gb : Î² â†’ Î² â†’ Î²}

protected theorem eq (h : Semiconjâ‚‚ f ga gb) (x y : Î±) : f (ga x y) = gb (f x) (f y) :=
  h x y
#align function.semiconjâ‚‚.eq Function.Semiconjâ‚‚.eq

protected theorem comp_eq (h : Semiconjâ‚‚ f ga gb) : bicompr f ga = bicompl gb f f :=
  funext fun x â†¦ funext <| h x
#align function.semiconjâ‚‚.comp_eq Function.Semiconjâ‚‚.comp_eq

theorem id_left (op : Î± â†’ Î± â†’ Î±) : Semiconjâ‚‚ id op op := fun _ _ â†¦ rfl
#align function.semiconjâ‚‚.id_left Function.Semiconjâ‚‚.id_left

theorem comp {f' : Î² â†’ Î³} {gc : Î³ â†’ Î³ â†’ Î³} (hf' : Semiconjâ‚‚ f' gb gc) (hf : Semiconjâ‚‚ f ga gb) :
    Semiconjâ‚‚ (f' âˆ˜ f) ga gc := fun x y â†¦ by simp only [hf'.eq, hf.eq, comp_apply]
                                             -- ðŸŽ‰ no goals
#align function.semiconjâ‚‚.comp Function.Semiconjâ‚‚.comp

theorem isAssociative_right [IsAssociative Î± ga] (h : Semiconjâ‚‚ f ga gb) (h_surj : Surjective f) :
    IsAssociative Î² gb :=
  âŸ¨h_surj.forallâ‚ƒ.2 fun xâ‚ xâ‚‚ xâ‚ƒ â†¦ by simp only [â† h.eq, @IsAssociative.assoc _ ga]âŸ©
                                      -- ðŸŽ‰ no goals
#align function.semiconjâ‚‚.is_associative_right Function.Semiconjâ‚‚.isAssociative_right

theorem isAssociative_left [IsAssociative Î² gb] (h : Semiconjâ‚‚ f ga gb) (h_inj : Injective f) :
    IsAssociative Î± ga :=
  âŸ¨fun xâ‚ xâ‚‚ xâ‚ƒ â†¦ h_inj <| by simp only [h.eq, @IsAssociative.assoc _ gb]âŸ©
                              -- ðŸŽ‰ no goals
#align function.semiconjâ‚‚.is_associative_left Function.Semiconjâ‚‚.isAssociative_left

theorem isIdempotent_right [IsIdempotent Î± ga] (h : Semiconjâ‚‚ f ga gb) (h_surj : Surjective f) :
    IsIdempotent Î² gb :=
  âŸ¨h_surj.forall.2 fun x â†¦ by simp only [â† h.eq, @IsIdempotent.idempotent _ ga]âŸ©
                              -- ðŸŽ‰ no goals
#align function.semiconjâ‚‚.is_idempotent_right Function.Semiconjâ‚‚.isIdempotent_right

theorem isIdempotent_left [IsIdempotent Î² gb] (h : Semiconjâ‚‚ f ga gb) (h_inj : Injective f) :
    IsIdempotent Î± ga :=
  âŸ¨fun x â†¦ h_inj <| by rw [h.eq, @IsIdempotent.idempotent _ gb]âŸ©
                       -- ðŸŽ‰ no goals
#align function.semiconjâ‚‚.is_idempotent_left Function.Semiconjâ‚‚.isIdempotent_left

end Semiconjâ‚‚

end Function
