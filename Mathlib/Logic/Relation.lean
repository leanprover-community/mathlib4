/-
Copyright (c) 2018 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
import Mathlib.Logic.Relator
import Mathlib.Init.Propext
import Mathlib.Init.Data.Quot
import Mathlib.Tactic.Common

#align_import logic.relation from "leanprover-community/mathlib"@"c4658a649d216f57e99621708b09dcb3dcccbd23"

/-!
# Relation closures

This file defines the reflexive, transitive, and reflexive transitive closures of relations.
It also proves some basic results on definitions such as `EqvGen`.

Note that this is about unbundled relations, that is terms of types of the form `Î± â†’ Î² â†’ Prop`. For
the bundled version, see `Rel`.

## Definitions

* `Relation.ReflGen`: Reflexive closure. `ReflGen r` relates everything `r` related, plus for all
  `a` it relates `a` with itself. So `ReflGen r a b â†” r a b âˆ¨ a = b`.
* `Relation.TransGen`: Transitive closure. `TransGen r` relates everything `r` related
  transitively. So `TransGen r a b â†” âˆƒ xâ‚€ ... xâ‚™, r a xâ‚€ âˆ§ r xâ‚€ xâ‚ âˆ§ ... âˆ§ r xâ‚™ b`.
* `Relation.ReflTransGen`: Reflexive transitive closure. `ReflTransGen r` relates everything
  `r` related transitively, plus for all `a` it relates `a` with itself. So
  `ReflTransGen r a b â†” (âˆƒ xâ‚€ ... xâ‚™, r a xâ‚€ âˆ§ r xâ‚€ xâ‚ âˆ§ ... âˆ§ r xâ‚™ b) âˆ¨ a = b`. It is the same as
  the reflexive closure of the transitive closure, or the transitive closure of the reflexive
  closure. In terms of rewriting systems, this means that `a` can be rewritten to `b` in a number of
  rewrites.
* `Relation.Comp`:  Relation composition. We provide notation `âˆ˜r`. For `r : Î± â†’ Î² â†’ Prop` and
  `s : Î² â†’ Î³ â†’ Prop`, `r âˆ˜r s`relates `a : Î±` and `c : Î³` iff there exists `b : Î²` that's related to
  both.
* `Relation.Map`: Image of a relation under a pair of maps. For `r : Î± â†’ Î² â†’ Prop`, `f : Î± â†’ Î³`,
  `g : Î² â†’ Î´`, `Map r f g` is the relation `Î³ â†’ Î´ â†’ Prop` relating `f a` and `g b` for all `a`, `b`
  related by `r`.
* `Relation.Join`: Join of a relation. For `r : Î± â†’ Î± â†’ Prop`, `Join r a b â†” âˆƒ c, r a c âˆ§ r b c`. In
  terms of rewriting systems, this means that `a` and `b` can be rewritten to the same term.
-/


open Function

variable {Î± Î² Î³ Î´ : Type*}

section NeImp

variable {r : Î± â†’ Î± â†’ Prop}

theorem IsRefl.reflexive [IsRefl Î± r] : Reflexive r := fun x â†¦ IsRefl.refl x
#align is_refl.reflexive IsRefl.reflexive

/-- To show a reflexive relation `r : Î± â†’ Î± â†’ Prop` holds over `x y : Î±`,
it suffices to show it holds when `x â‰  y`. -/
theorem Reflexive.rel_of_ne_imp (h : Reflexive r) {x y : Î±} (hr : x â‰  y â†’ r x y) : r x y := by
  by_cases hxy : x = y
  -- âŠ¢ r x y
  Â· exact hxy â–¸ h x
    -- ğŸ‰ no goals
  Â· exact hr hxy
    -- ğŸ‰ no goals
#align reflexive.rel_of_ne_imp Reflexive.rel_of_ne_imp


/-- If a reflexive relation `r : Î± â†’ Î± â†’ Prop` holds over `x y : Î±`,
then it holds whether or not `x â‰  y`. -/
theorem Reflexive.ne_imp_iff (h : Reflexive r) {x y : Î±} : x â‰  y â†’ r x y â†” r x y :=
  âŸ¨h.rel_of_ne_imp, fun hr _ â†¦ hrâŸ©
#align reflexive.ne_imp_iff Reflexive.ne_imp_iff

/-- If a reflexive relation `r : Î± â†’ Î± â†’ Prop` holds over `x y : Î±`,
then it holds whether or not `x â‰  y`. Unlike `Reflexive.ne_imp_iff`, this uses `[IsRefl Î± r]`. -/
theorem reflexive_ne_imp_iff [IsRefl Î± r] {x y : Î±} : x â‰  y â†’ r x y â†” r x y :=
  IsRefl.reflexive.ne_imp_iff
#align reflexive_ne_imp_iff reflexive_ne_imp_iff

protected theorem Symmetric.iff (H : Symmetric r) (x y : Î±) : r x y â†” r y x :=
  âŸ¨fun h â†¦ H h, fun h â†¦ H hâŸ©
#align symmetric.iff Symmetric.iff

theorem Symmetric.flip_eq (h : Symmetric r) : flip r = r :=
  funextâ‚‚ fun _ _ â†¦ propext <| h.iff _ _
#align symmetric.flip_eq Symmetric.flip_eq

theorem Symmetric.swap_eq : Symmetric r â†’ swap r = r :=
  Symmetric.flip_eq
#align symmetric.swap_eq Symmetric.swap_eq

theorem flip_eq_iff : flip r = r â†” Symmetric r :=
  âŸ¨fun h _ _ â†¦ (congr_funâ‚‚ h _ _).mp, Symmetric.flip_eqâŸ©
#align flip_eq_iff flip_eq_iff

theorem swap_eq_iff : swap r = r â†” Symmetric r :=
  flip_eq_iff
#align swap_eq_iff swap_eq_iff

end NeImp

section Comap

variable {r : Î² â†’ Î² â†’ Prop}

theorem Reflexive.comap (h : Reflexive r) (f : Î± â†’ Î²) : Reflexive (r on f) := fun a â†¦ h (f a)
#align reflexive.comap Reflexive.comap

theorem Symmetric.comap (h : Symmetric r) (f : Î± â†’ Î²) : Symmetric (r on f) := fun _ _ hab â†¦ h hab
#align symmetric.comap Symmetric.comap

theorem Transitive.comap (h : Transitive r) (f : Î± â†’ Î²) : Transitive (r on f) :=
  fun _ _ _ hab hbc â†¦ h hab hbc
#align transitive.comap Transitive.comap

theorem Equivalence.comap (h : Equivalence r) (f : Î± â†’ Î²) : Equivalence (r on f) :=
  âŸ¨h.reflexive.comap f, @(h.symmetric.comap f), @(h.transitive.comap f)âŸ©
#align equivalence.comap Equivalence.comap

end Comap

namespace Relation

section Comp

variable {r : Î± â†’ Î² â†’ Prop} {p : Î² â†’ Î³ â†’ Prop} {q : Î³ â†’ Î´ â†’ Prop}

/-- The composition of two relations, yielding a new relation.  The result
relates a term of `Î±` and a term of `Î³` if there is an intermediate
term of `Î²` related to both.
-/
def Comp (r : Î± â†’ Î² â†’ Prop) (p : Î² â†’ Î³ â†’ Prop) (a : Î±) (c : Î³) : Prop :=
  âˆƒ b, r a b âˆ§ p b c
#align relation.comp Relation.Comp

@[inherit_doc]
local infixr:80 " âˆ˜r " => Relation.Comp

theorem comp_eq : r âˆ˜r (Â· = Â·) = r :=
  funext fun _ â†¦ funext fun b â†¦ propext <|
  Iff.intro (fun âŸ¨_, h, EqâŸ© â†¦ Eq â–¸ h) fun h â†¦ âŸ¨b, h, rflâŸ©
#align relation.comp_eq Relation.comp_eq

theorem eq_comp : (Â· = Â·) âˆ˜r r = r :=
  funext fun a â†¦ funext fun _ â†¦ propext <|
  Iff.intro (fun âŸ¨_, Eq, hâŸ© â†¦ Eq.symm â–¸ h) fun h â†¦ âŸ¨a, rfl, hâŸ©
#align relation.eq_comp Relation.eq_comp

theorem iff_comp {r : Prop â†’ Î± â†’ Prop} : (Â· â†” Â·) âˆ˜r r = r := by
  have : (Â· â†” Â·) = (Â· = Â·) := by funext a b; exact iff_eq_eq
  -- âŠ¢ (fun x x_1 => x â†” x_1) âˆ˜r r = r
  rw [this, eq_comp]
  -- ğŸ‰ no goals
#align relation.iff_comp Relation.iff_comp

theorem comp_iff {r : Î± â†’ Prop â†’ Prop} : r âˆ˜r (Â· â†” Â·) = r := by
  have : (Â· â†” Â·) = (Â· = Â·) := by funext a b; exact iff_eq_eq
  -- âŠ¢ (r âˆ˜r fun x x_1 => x â†” x_1) = r
  rw [this, comp_eq]
  -- ğŸ‰ no goals
#align relation.comp_iff Relation.comp_iff

theorem comp_assoc : (r âˆ˜r p) âˆ˜r q = r âˆ˜r p âˆ˜r q := by
  funext a d
  -- âŠ¢ ((r âˆ˜r p) âˆ˜r q) a d = (r âˆ˜r p âˆ˜r q) a d
  apply propext
  -- âŠ¢ ((r âˆ˜r p) âˆ˜r q) a d â†” (r âˆ˜r p âˆ˜r q) a d
  constructor
  -- âŠ¢ ((r âˆ˜r p) âˆ˜r q) a d â†’ (r âˆ˜r p âˆ˜r q) a d
  exact fun âŸ¨c, âŸ¨b, hab, hbcâŸ©, hcdâŸ© â†¦ âŸ¨b, hab, c, hbc, hcdâŸ©
  -- âŠ¢ (r âˆ˜r p âˆ˜r q) a d â†’ ((r âˆ˜r p) âˆ˜r q) a d
  exact fun âŸ¨b, hab, c, hbc, hcdâŸ© â†¦ âŸ¨c, âŸ¨b, hab, hbcâŸ©, hcdâŸ©
  -- ğŸ‰ no goals
#align relation.comp_assoc Relation.comp_assoc

theorem flip_comp : flip (r âˆ˜r p) = flip p âˆ˜r flip r := by
  funext c a
  -- âŠ¢ flip (r âˆ˜r p) c a = (flip p âˆ˜r flip r) c a
  apply propext
  -- âŠ¢ flip (r âˆ˜r p) c a â†” (flip p âˆ˜r flip r) c a
  constructor
  -- âŠ¢ flip (r âˆ˜r p) c a â†’ (flip p âˆ˜r flip r) c a
  exact fun âŸ¨b, hab, hbcâŸ© â†¦ âŸ¨b, hbc, habâŸ©
  -- âŠ¢ (flip p âˆ˜r flip r) c a â†’ flip (r âˆ˜r p) c a
  exact fun âŸ¨b, hbc, habâŸ© â†¦ âŸ¨b, hab, hbcâŸ©
  -- ğŸ‰ no goals
#align relation.flip_comp Relation.flip_comp

end Comp

section Fibration

variable (rÎ± : Î± â†’ Î± â†’ Prop) (rÎ² : Î² â†’ Î² â†’ Prop) (f : Î± â†’ Î²)

/-- A function `f : Î± â†’ Î²` is a fibration between the relation `rÎ±` and `rÎ²` if for all
  `a : Î±` and `b : Î²`, whenever `b : Î²` and `f a` are related by `rÎ²`, `b` is the image
  of some `a' : Î±` under `f`, and `a'` and `a` are related by `rÎ±`. -/
def Fibration :=
  âˆ€ â¦ƒa bâ¦„, rÎ² b (f a) â†’ âˆƒ a', rÎ± a' a âˆ§ f a' = b
#align relation.fibration Relation.Fibration

variable {rÎ± rÎ²}

/-- If `f : Î± â†’ Î²` is a fibration between relations `rÎ±` and `rÎ²`, and `a : Î±` is
  accessible under `rÎ±`, then `f a` is accessible under `rÎ²`. -/
theorem _root_.Acc.of_fibration (fib : Fibration rÎ± rÎ² f) {a} (ha : Acc rÎ± a) : Acc rÎ² (f a) := by
  induction' ha with a _ ih
  -- âŠ¢ Acc rÎ² (f a)
  refine' Acc.intro (f a) fun b hr â†¦ _
  -- âŠ¢ Acc rÎ² b
  obtain âŸ¨a', hr', rflâŸ© := fib hr
  -- âŠ¢ Acc rÎ² (f a')
  exact ih a' hr'
  -- ğŸ‰ no goals
#align acc.of_fibration Acc.of_fibration

theorem _root_.Acc.of_downward_closed (dc : âˆ€ {a b}, rÎ² b (f a) â†’ âˆƒ c, f c = b) (a : Î±)
    (ha : Acc (InvImage rÎ² f) a) : Acc rÎ² (f a) :=
  ha.of_fibration f fun a _ h â†¦
    let âŸ¨a', heâŸ© := dc h
    -- porting note: Lean 3 did not need the motive
    âŸ¨a', he.substr (p := Î» x => rÎ² x (f a)) h, heâŸ©
#align acc.of_downward_closed Acc.of_downward_closed

end Fibration

/-- The map of a relation `r` through a pair of functions pushes the
relation to the codomains of the functions.  The resulting relation is
defined by having pairs of terms related if they have preimages
related by `r`.
-/
protected def Map (r : Î± â†’ Î² â†’ Prop) (f : Î± â†’ Î³) (g : Î² â†’ Î´) : Î³ â†’ Î´ â†’ Prop := fun c d â†¦
  âˆƒ a b, r a b âˆ§ f a = c âˆ§ g b = d
#align relation.map Relation.Map

variable {r : Î± â†’ Î± â†’ Prop} {a b c d : Î±}

/-- `ReflTransGen r`: reflexive transitive closure of `r` -/
@[mk_iff ReflTransGen.cases_tail_iff]
inductive ReflTransGen (r : Î± â†’ Î± â†’ Prop) (a : Î±) : Î± â†’ Prop
  | refl : ReflTransGen r a a
  | tail {b c} : ReflTransGen r a b â†’ r b c â†’ ReflTransGen r a c
#align relation.refl_trans_gen Relation.ReflTransGen
#align relation.refl_trans_gen.cases_tail_iff Relation.ReflTransGen.cases_tail_iff

attribute [refl] ReflTransGen.refl

/-- `ReflGen r`: reflexive closure of `r` -/
@[mk_iff reflGen_iff]
inductive ReflGen (r : Î± â†’ Î± â†’ Prop) (a : Î±) : Î± â†’ Prop
  | refl : ReflGen r a a
  | single {b} : r a b â†’ ReflGen r a b
#align relation.refl_gen Relation.ReflGen

#align relation.refl_gen_iff Relation.reflGen_iff

/-- `TransGen r`: transitive closure of `r` -/
@[mk_iff transGen_iff]
inductive TransGen (r : Î± â†’ Î± â†’ Prop) (a : Î±) : Î± â†’ Prop
  | single {b} : r a b â†’ TransGen r a b
  | tail {b c} : TransGen r a b â†’ r b c â†’ TransGen r a c
#align relation.trans_gen Relation.TransGen

#align relation.trans_gen_iff Relation.transGen_iff

attribute [refl] ReflGen.refl

namespace ReflGen

theorem to_reflTransGen : âˆ€ {a b}, ReflGen r a b â†’ ReflTransGen r a b
  | a, _, refl => by rfl
                     -- ğŸ‰ no goals
  | a, b, single h => ReflTransGen.tail ReflTransGen.refl h
#align relation.refl_gen.to_refl_trans_gen Relation.ReflGen.to_reflTransGen

theorem mono {p : Î± â†’ Î± â†’ Prop} (hp : âˆ€ a b, r a b â†’ p a b) : âˆ€ {a b}, ReflGen r a b â†’ ReflGen p a b
  | a, _, ReflGen.refl => by rfl
                             -- ğŸ‰ no goals
  | a, b, single h => single (hp a b h)
#align relation.refl_gen.mono Relation.ReflGen.mono

instance : IsRefl Î± (ReflGen r) :=
  âŸ¨@refl Î± râŸ©

end ReflGen

namespace ReflTransGen

@[trans]
theorem trans (hab : ReflTransGen r a b) (hbc : ReflTransGen r b c) : ReflTransGen r a c := by
  induction hbc
  -- âŠ¢ ReflTransGen r a b
  case refl => assumption
  -- âŠ¢ ReflTransGen r a câœ
  -- ğŸ‰ no goals
  case tail c d _ hcd hac => exact hac.tail hcd
  -- ğŸ‰ no goals
  -- ğŸ‰ no goals
#align relation.refl_trans_gen.trans Relation.ReflTransGen.trans

theorem single (hab : r a b) : ReflTransGen r a b :=
  refl.tail hab
#align relation.refl_trans_gen.single Relation.ReflTransGen.single

theorem head (hab : r a b) (hbc : ReflTransGen r b c) : ReflTransGen r a c := by
  induction hbc
  -- âŠ¢ ReflTransGen r a b
  case refl => exact refl.tail hab
  -- âŠ¢ ReflTransGen r a câœ
  -- ğŸ‰ no goals
  case tail c d _ hcd hac => exact hac.tail hcd
  -- ğŸ‰ no goals
  -- ğŸ‰ no goals
#align relation.refl_trans_gen.head Relation.ReflTransGen.head

theorem symmetric (h : Symmetric r) : Symmetric (ReflTransGen r) := by
  intro x y h
  -- âŠ¢ ReflTransGen r y x
  induction' h with z w _ b c
  -- âŠ¢ ReflTransGen r x x
  Â· rfl
    -- ğŸ‰ no goals
  Â· apply Relation.ReflTransGen.head (h b) c
    -- ğŸ‰ no goals
#align relation.refl_trans_gen.symmetric Relation.ReflTransGen.symmetric

theorem cases_tail : ReflTransGen r a b â†’ b = a âˆ¨ âˆƒ c, ReflTransGen r a c âˆ§ r c b :=
  (cases_tail_iff r a b).1
#align relation.refl_trans_gen.cases_tail Relation.ReflTransGen.cases_tail

@[elab_as_elim]
theorem head_induction_on {P : âˆ€ a : Î±, ReflTransGen r a b â†’ Prop} {a : Î±} (h : ReflTransGen r a b)
    (refl : P b refl)
    (head : âˆ€ {a c} (h' : r a c) (h : ReflTransGen r c b), P c h â†’ P a (h.head h')) : P a h := by
  induction h
  -- âŠ¢ P a (_ : ReflTransGen r a a)
  case refl => exact refl
  -- âŠ¢ P a (_ : ReflTransGen r a câœ)
  -- ğŸ‰ no goals
  case tail b c _ hbc ih =>
  -- Porting note: Lean 3 figured out the motive and `apply ih` worked
  refine @ih (Î» {a : Î±} (hab : ReflTransGen r a b) => P a (ReflTransGen.tail hab hbc)) ?_ ?_
  { exact head hbc _ refl }
  { exact fun h1 h2 â†¦ head h1 (h2.tail hbc) }
#align relation.refl_trans_gen.head_induction_on Relation.ReflTransGen.head_induction_on

@[elab_as_elim]
theorem trans_induction_on {P : âˆ€ {a b : Î±}, ReflTransGen r a b â†’ Prop} {a b : Î±}
    (h : ReflTransGen r a b) (ihâ‚ : âˆ€ a, @P a a refl) (ihâ‚‚ : âˆ€ {a b} (h : r a b), P (single h))
    (ihâ‚ƒ : âˆ€ {a b c} (hâ‚ : ReflTransGen r a b) (hâ‚‚ : ReflTransGen r b c), P hâ‚ â†’ P hâ‚‚ â†’
     P (hâ‚.trans hâ‚‚)) : P h := by
  induction h
  -- âŠ¢ P (_ : ReflTransGen r a a)
  case refl => exact ihâ‚ a
  -- âŠ¢ P (_ : ReflTransGen r a câœ)
  -- ğŸ‰ no goals
  case tail b c hab hbc ih => exact ihâ‚ƒ hab (single hbc) ih (ihâ‚‚ hbc)
  -- ğŸ‰ no goals
  -- ğŸ‰ no goals
#align relation.refl_trans_gen.trans_induction_on Relation.ReflTransGen.trans_induction_on

theorem cases_head (h : ReflTransGen r a b) : a = b âˆ¨ âˆƒ c, r a c âˆ§ ReflTransGen r c b := by
  induction h using Relation.ReflTransGen.head_induction_on
  -- âŠ¢ b = b âˆ¨ âˆƒ c, r b c âˆ§ ReflTransGen r c b
  Â· left
    -- âŠ¢ b = b
    rfl
    -- ğŸ‰ no goals
  Â· right
    -- âŠ¢ âˆƒ c, r aâœÂ¹ c âˆ§ ReflTransGen r c b
    exact âŸ¨_, by assumption, by assumptionâŸ©;
    -- ğŸ‰ no goals
#align relation.refl_trans_gen.cases_head Relation.ReflTransGen.cases_head

theorem cases_head_iff : ReflTransGen r a b â†” a = b âˆ¨ âˆƒ c, r a c âˆ§ ReflTransGen r c b := by
  use cases_head
  -- âŠ¢ (a = b âˆ¨ âˆƒ c, r a c âˆ§ ReflTransGen r c b) â†’ ReflTransGen r a b
  rintro (rfl | âŸ¨c, hac, hcbâŸ©)
  -- âŠ¢ ReflTransGen r a a
  Â· rfl
    -- ğŸ‰ no goals
  Â· exact head hac hcb
    -- ğŸ‰ no goals
#align relation.refl_trans_gen.cases_head_iff Relation.ReflTransGen.cases_head_iff

theorem total_of_right_unique (U : Relator.RightUnique r) (ab : ReflTransGen r a b)
    (ac : ReflTransGen r a c) : ReflTransGen r b c âˆ¨ ReflTransGen r c b := by
  induction' ab with b d _ bd IH
  -- âŠ¢ ReflTransGen r a c âˆ¨ ReflTransGen r c a
  Â· exact Or.inl ac
    -- ğŸ‰ no goals
  Â· rcases IH with (IH | IH)
    -- âŠ¢ ReflTransGen r d c âˆ¨ ReflTransGen r c d
    Â· rcases cases_head IH with (rfl | âŸ¨e, be, ecâŸ©)
      -- âŠ¢ ReflTransGen r d b âˆ¨ ReflTransGen r b d
      Â· exact Or.inr (single bd)
        -- ğŸ‰ no goals
      Â· cases U bd be
        -- âŠ¢ ReflTransGen r d c âˆ¨ ReflTransGen r c d
        exact Or.inl ec
        -- ğŸ‰ no goals
    Â· exact Or.inr (IH.tail bd)
      -- ğŸ‰ no goals
#align relation.refl_trans_gen.total_of_right_unique Relation.ReflTransGen.total_of_right_unique

end ReflTransGen

namespace TransGen

theorem to_reflTransGen {a b} (h : TransGen r a b) : ReflTransGen r a b := by
  induction' h with b h b c _ bc ab
  -- âŠ¢ ReflTransGen r a b
  exact ReflTransGen.single h
  -- âŠ¢ ReflTransGen r a c
  exact ReflTransGen.tail ab bc
  -- ğŸ‰ no goals
-- porting note: in Lean 3 this function was called `to_refl` which seems wrong.
#align relation.trans_gen.to_refl Relation.TransGen.to_reflTransGen

theorem trans_left (hab : TransGen r a b) (hbc : ReflTransGen r b c) : TransGen r a c := by
  induction hbc
  -- âŠ¢ TransGen r a b
  case refl => assumption
  -- âŠ¢ TransGen r a câœ
  -- ğŸ‰ no goals
  case tail c d _ hcd hac => exact hac.tail hcd
  -- ğŸ‰ no goals
  -- ğŸ‰ no goals
#align relation.trans_gen.trans_left Relation.TransGen.trans_left

instance : Trans (TransGen r) (ReflTransGen r) (TransGen r) :=
  âŸ¨trans_leftâŸ©

@[trans]
theorem trans (hab : TransGen r a b) (hbc : TransGen r b c) : TransGen r a c :=
  trans_left hab hbc.to_reflTransGen
#align relation.trans_gen.trans Relation.TransGen.trans

instance : Trans (TransGen r) (TransGen r) (TransGen r) :=
  âŸ¨transâŸ©

theorem head' (hab : r a b) (hbc : ReflTransGen r b c) : TransGen r a c :=
  trans_left (single hab) hbc
#align relation.trans_gen.head' Relation.TransGen.head'

theorem tail' (hab : ReflTransGen r a b) (hbc : r b c) : TransGen r a c := by
  induction hab generalizing c
  -- âŠ¢ TransGen r a c
  case refl => exact single hbc
  -- âŠ¢ TransGen r a c
  -- ğŸ‰ no goals
  case tail _ _ _ hdb IH => exact tail (IH hdb) hbc
  -- ğŸ‰ no goals
  -- ğŸ‰ no goals
#align relation.trans_gen.tail' Relation.TransGen.tail'

theorem head (hab : r a b) (hbc : TransGen r b c) : TransGen r a c :=
  head' hab hbc.to_reflTransGen
#align relation.trans_gen.head Relation.TransGen.head

@[elab_as_elim]
theorem head_induction_on {P : âˆ€ a : Î±, TransGen r a b â†’ Prop} {a : Î±} (h : TransGen r a b)
    (base : âˆ€ {a} (h : r a b), P a (single h))
    (ih : âˆ€ {a c} (h' : r a c) (h : TransGen r c b), P c h â†’ P a (h.head h')) : P a h := by
  induction h
  -- âŠ¢ P a (_ : TransGen r a bâœ)
  case single a h => exact base h
  -- âŠ¢ P a (_ : TransGen r a câœ)
  -- ğŸ‰ no goals
  case tail b c _ hbc h_ih =>
  -- Lean 3 could figure out the motive and `apply h_ih` worked
  refine @h_ih (Î» {a : Î±} (hab : @TransGen Î± r a b) => P a (TransGen.tail hab hbc)) ?_ ?_;
  exact fun h â†¦ ih h (single hbc) (base hbc)
  exact fun hab hbc â†¦ ih hab _
#align relation.trans_gen.head_induction_on Relation.TransGen.head_induction_on

@[elab_as_elim]
theorem trans_induction_on {P : âˆ€ {a b : Î±}, TransGen r a b â†’ Prop} {a b : Î±} (h : TransGen r a b)
    (base : âˆ€ {a b} (h : r a b), P (single h))
    (ih : âˆ€ {a b c} (hâ‚ : TransGen r a b) (hâ‚‚ : TransGen r b c), P hâ‚ â†’ P hâ‚‚ â†’ P (hâ‚.trans hâ‚‚)) :
    P h := by
  induction h with
  | single h => exact base h
  | tail hab hbc h_ih => exact ih hab (single hbc) h_ih (base hbc)
#align relation.trans_gen.trans_induction_on Relation.TransGen.trans_induction_on

theorem trans_right (hab : ReflTransGen r a b) (hbc : TransGen r b c) : TransGen r a c := by
  induction hbc
  -- âŠ¢ TransGen r a bâœ
  case single c hbc => exact tail' hab hbc
  -- âŠ¢ TransGen r a câœ
  -- ğŸ‰ no goals
  case tail c d _ hcd hac => exact hac.tail hcd
  -- ğŸ‰ no goals
  -- ğŸ‰ no goals
#align relation.trans_gen.trans_right Relation.TransGen.trans_right

instance : Trans (ReflTransGen r) (TransGen r) (TransGen r) :=
  âŸ¨trans_rightâŸ©

theorem tail'_iff : TransGen r a c â†” âˆƒ b, ReflTransGen r a b âˆ§ r b c := by
  refine' âŸ¨fun h â†¦ _, fun âŸ¨b, hab, hbcâŸ© â†¦ tail' hab hbcâŸ©
  -- âŠ¢ âˆƒ b, ReflTransGen r a b âˆ§ r b c
  cases' h with _ hac b _ hab hbc
  -- âŠ¢ âˆƒ b, ReflTransGen r a b âˆ§ r b c
  Â· exact âŸ¨_, by rfl, hacâŸ©
    -- ğŸ‰ no goals
  Â· exact âŸ¨_, hab.to_reflTransGen, hbcâŸ©
    -- ğŸ‰ no goals
#align relation.trans_gen.tail'_iff Relation.TransGen.tail'_iff

theorem head'_iff : TransGen r a c â†” âˆƒ b, r a b âˆ§ ReflTransGen r b c := by
  refine' âŸ¨fun h â†¦ _, fun âŸ¨b, hab, hbcâŸ© â†¦ head' hab hbcâŸ©
  -- âŠ¢ âˆƒ b, r a b âˆ§ ReflTransGen r b c
  induction h
  -- âŠ¢ âˆƒ b, r a b âˆ§ ReflTransGen r b bâœ
  case single c hac => exact âŸ¨_, hac, by rflâŸ©
  -- âŠ¢ âˆƒ b, r a b âˆ§ ReflTransGen r b câœ
  -- ğŸ‰ no goals
  case tail b c _ hbc IH =>
  rcases IH with âŸ¨d, had, hdbâŸ©
  exact âŸ¨_, had, hdb.tail hbcâŸ©
#align relation.trans_gen.head'_iff Relation.TransGen.head'_iff

end TransGen

theorem _root_.Acc.TransGen (h : Acc r a) : Acc (TransGen r) a := by
  induction' h with x _ H
  -- âŠ¢ Acc (Relation.TransGen r) x
  refine' Acc.intro x fun y hy â†¦ _
  -- âŠ¢ Acc (Relation.TransGen r) y
  cases' hy with _ hyx z _ hyz hzx
  -- âŠ¢ Acc (Relation.TransGen r) y
  exacts [H y hyx, (H z hzx).inv hyz]
  -- ğŸ‰ no goals
#align acc.trans_gen Acc.TransGen

theorem _root_.acc_transGen_iff : Acc (TransGen r) a â†” Acc r a :=
  âŸ¨Subrelation.accessible TransGen.single, Acc.TransGenâŸ©
#align acc_trans_gen_iff acc_transGen_iff

theorem _root_.WellFounded.transGen (h : WellFounded r) : WellFounded (TransGen r) :=
  âŸ¨fun a â†¦ (h.apply a).TransGenâŸ©
#align well_founded.trans_gen WellFounded.transGen

section reflGen

lemma reflGen_eq_self (hr : Reflexive r) : ReflGen r = r := by
  ext x y
  -- âŠ¢ ReflGen r x y â†” r x y
  simpa only [reflGen_iff, or_iff_right_iff_imp] using fun h â†¦ h â–¸ hr y
  -- ğŸ‰ no goals

lemma reflexive_reflGen : Reflexive (ReflGen r) := fun _ â†¦ .refl

lemma reflGen_minimal {r' : Î± â†’ Î± â†’ Prop} (hr' : Reflexive r') (h : âˆ€ x y, r x y â†’ r' x y) {x y : Î±}
    (hxy : ReflGen r x y) : r' x y := by
  simpa [reflGen_eq_self hr'] using ReflGen.mono h hxy
  -- ğŸ‰ no goals

end reflGen

section TransGen

theorem transGen_eq_self (trans : Transitive r) : TransGen r = r :=
  funext fun a â†¦ funext fun b â†¦ propext <|
    âŸ¨fun h â†¦ by
      induction h
      -- âŠ¢ r a bâœ
      case single _ hc => exact hc
      -- âŠ¢ r a câœ
      -- ğŸ‰ no goals
      case tail c d _ hcd hac => exact trans hac hcd, TransGen.singleâŸ©
      -- ğŸ‰ no goals
      -- ğŸ‰ no goals
#align relation.trans_gen_eq_self Relation.transGen_eq_self

theorem transitive_transGen : Transitive (TransGen r) := fun _ _ _ â†¦ TransGen.trans
#align relation.transitive_trans_gen Relation.transitive_transGen

instance : IsTrans Î± (TransGen r) :=
  âŸ¨@TransGen.trans Î± râŸ©

theorem transGen_idem : TransGen (TransGen r) = TransGen r :=
  transGen_eq_self transitive_transGen
#align relation.trans_gen_idem Relation.transGen_idem

theorem TransGen.lift {p : Î² â†’ Î² â†’ Prop} {a b : Î±} (f : Î± â†’ Î²) (h : âˆ€ a b, r a b â†’ p (f a) (f b))
    (hab : TransGen r a b) : TransGen p (f a) (f b) := by
  induction hab
  -- âŠ¢ TransGen p (f a) (f bâœ)
  case single c hac => exact TransGen.single (h a c hac)
  -- âŠ¢ TransGen p (f a) (f câœ)
  -- ğŸ‰ no goals
  case tail c d _ hcd hac => exact TransGen.tail hac (h c d hcd)
  -- ğŸ‰ no goals
  -- ğŸ‰ no goals
#align relation.trans_gen.lift Relation.TransGen.lift

theorem TransGen.lift' {p : Î² â†’ Î² â†’ Prop} {a b : Î±} (f : Î± â†’ Î²)
    (h : âˆ€ a b, r a b â†’ TransGen p (f a) (f b)) (hab : TransGen r a b) :
    TransGen p (f a) (f b) := by
simpa [transGen_idem] using hab.lift f h
-- ğŸ‰ no goals
#align relation.trans_gen.lift' Relation.TransGen.lift'

theorem TransGen.closed {p : Î± â†’ Î± â†’ Prop} :
    (âˆ€ a b, r a b â†’ TransGen p a b) â†’ TransGen r a b â†’ TransGen p a b :=
  TransGen.lift' id
#align relation.trans_gen.closed Relation.TransGen.closed

theorem TransGen.mono {p : Î± â†’ Î± â†’ Prop} :
    (âˆ€ a b, r a b â†’ p a b) â†’ TransGen r a b â†’ TransGen p a b :=
  TransGen.lift id
#align relation.trans_gen.mono Relation.TransGen.mono

lemma transGen_minimal {r' : Î± â†’ Î± â†’ Prop} (hr' : Transitive r') (h : âˆ€ x y, r x y â†’ r' x y)
    {x y : Î±} (hxy : TransGen r x y) : r' x y := by
  simpa [transGen_eq_self hr'] using TransGen.mono h hxy
  -- ğŸ‰ no goals

theorem TransGen.swap (h : TransGen r b a) : TransGen (swap r) a b := by
  induction' h with b h b c _ hbc ih
  -- âŠ¢ TransGen (Function.swap r) b bâœ
  Â· exact TransGen.single h
    -- ğŸ‰ no goals
  Â· exact ih.head hbc
    -- ğŸ‰ no goals
#align relation.trans_gen.swap Relation.TransGen.swap

theorem transGen_swap : TransGen (swap r) a b â†” TransGen r b a :=
  âŸ¨TransGen.swap, TransGen.swapâŸ©
#align relation.trans_gen_swap Relation.transGen_swap

end TransGen

section ReflTransGen

open ReflTransGen

theorem reflTransGen_iff_eq (h : âˆ€ b, Â¬r a b) : ReflTransGen r a b â†” b = a := by
  rw [cases_head_iff]; simp [h, eq_comm]
  -- âŠ¢ (a = b âˆ¨ âˆƒ c, r a c âˆ§ ReflTransGen r c b) â†” b = a
                       -- ğŸ‰ no goals
#align relation.refl_trans_gen_iff_eq Relation.reflTransGen_iff_eq

theorem reflTransGen_iff_eq_or_transGen : ReflTransGen r a b â†” b = a âˆ¨ TransGen r a b := by
  refine' âŸ¨fun h â†¦ _, fun h â†¦ _âŸ©
  -- âŠ¢ b = a âˆ¨ TransGen r a b
  Â· cases' h with c _ hac hcb
    -- âŠ¢ a = a âˆ¨ TransGen r a a
    Â· exact Or.inl rfl
      -- ğŸ‰ no goals
    Â· exact Or.inr (TransGen.tail' hac hcb)
      -- ğŸ‰ no goals
  Â· rcases h with (rfl | h)
    -- âŠ¢ ReflTransGen r b b
    Â· rfl
      -- ğŸ‰ no goals
    Â· exact h.to_reflTransGen
      -- ğŸ‰ no goals
#align relation.refl_trans_gen_iff_eq_or_trans_gen Relation.reflTransGen_iff_eq_or_transGen

theorem ReflTransGen.lift {p : Î² â†’ Î² â†’ Prop} {a b : Î±} (f : Î± â†’ Î²)
    (h : âˆ€ a b, r a b â†’ p (f a) (f b)) (hab : ReflTransGen r a b) : ReflTransGen p (f a) (f b) :=
  ReflTransGen.trans_induction_on hab (fun _ â†¦ refl) (ReflTransGen.single âˆ˜ h _ _) fun _ _ â†¦ trans
#align relation.refl_trans_gen.lift Relation.ReflTransGen.lift

theorem ReflTransGen.mono {p : Î± â†’ Î± â†’ Prop} : (âˆ€ a b, r a b â†’ p a b) â†’
    ReflTransGen r a b â†’ ReflTransGen p a b :=
  ReflTransGen.lift id
#align relation.refl_trans_gen.mono Relation.ReflTransGen.mono

theorem reflTransGen_eq_self (refl : Reflexive r) (trans : Transitive r) : ReflTransGen r = r :=
  funext fun a â†¦ funext fun b â†¦ propext <|
    âŸ¨fun h â†¦ by
      induction' h with b c _ hâ‚‚ IH
      -- âŠ¢ r a a
      Â· apply refl
        -- ğŸ‰ no goals
      Â· exact trans IH hâ‚‚, singleâŸ©
        -- ğŸ‰ no goals
#align relation.refl_trans_gen_eq_self Relation.reflTransGen_eq_self

lemma reflTransGen_minimal {r' : Î± â†’ Î± â†’ Prop} (hrâ‚ : Reflexive r') (hrâ‚‚ : Transitive r')
    (h : âˆ€ x y, r x y â†’ r' x y) {x y : Î±} (hxy : ReflTransGen r x y) : r' x y := by
  simpa [reflTransGen_eq_self hrâ‚ hrâ‚‚] using ReflTransGen.mono h hxy
  -- ğŸ‰ no goals

theorem reflexive_reflTransGen : Reflexive (ReflTransGen r) := fun _ â†¦ refl
#align relation.reflexive_refl_trans_gen Relation.reflexive_reflTransGen

theorem transitive_reflTransGen : Transitive (ReflTransGen r) := fun _ _ _ â†¦ trans
#align relation.transitive_refl_trans_gen Relation.transitive_reflTransGen

instance : IsRefl Î± (ReflTransGen r) :=
  âŸ¨@ReflTransGen.refl Î± râŸ©

instance : IsTrans Î± (ReflTransGen r) :=
  âŸ¨@ReflTransGen.trans Î± râŸ©

theorem reflTransGen_idem : ReflTransGen (ReflTransGen r) = ReflTransGen r :=
  reflTransGen_eq_self reflexive_reflTransGen transitive_reflTransGen
#align relation.refl_trans_gen_idem Relation.reflTransGen_idem

theorem ReflTransGen.lift' {p : Î² â†’ Î² â†’ Prop} {a b : Î±} (f : Î± â†’ Î²)
    (h : âˆ€ a b, r a b â†’ ReflTransGen p (f a) (f b))
    (hab : ReflTransGen r a b) : ReflTransGen p (f a) (f b) := by
  simpa [reflTransGen_idem] using hab.lift f h
  -- ğŸ‰ no goals
#align relation.refl_trans_gen.lift' Relation.ReflTransGen.lift'

theorem reflTransGen_closed {p : Î± â†’ Î± â†’ Prop} :
    (âˆ€ a b, r a b â†’ ReflTransGen p a b) â†’ ReflTransGen r a b â†’ ReflTransGen p a b :=
  ReflTransGen.lift' id
#align relation.refl_trans_gen_closed Relation.reflTransGen_closed

theorem ReflTransGen.swap (h : ReflTransGen r b a) : ReflTransGen (swap r) a b := by
  induction' h with b c _ hbc ih
  -- âŠ¢ ReflTransGen (Function.swap r) b b
  Â· rfl
    -- ğŸ‰ no goals
  Â· exact ih.head hbc
    -- ğŸ‰ no goals
#align relation.refl_trans_gen.swap Relation.ReflTransGen.swap

theorem reflTransGen_swap : ReflTransGen (swap r) a b â†” ReflTransGen r b a :=
  âŸ¨ReflTransGen.swap, ReflTransGen.swapâŸ©
#align relation.refl_trans_gen_swap Relation.reflTransGen_swap

@[simp] lemma reflGen_transGen : ReflGen (TransGen r) = ReflTransGen r := by
  ext x y
  -- âŠ¢ ReflGen (TransGen r) x y â†” ReflTransGen r x y
  simp_rw [reflTransGen_iff_eq_or_transGen, reflGen_iff]
  -- ğŸ‰ no goals

@[simp] lemma transGen_reflGen : TransGen (ReflGen r) = ReflTransGen r := by
  ext x y
  -- âŠ¢ TransGen (ReflGen r) x y â†” ReflTransGen r x y
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  -- âŠ¢ ReflTransGen r x y
  Â· simpa [reflTransGen_idem]
      using TransGen.to_reflTransGen <| TransGen.mono (fun _ _ â†¦ ReflGen.to_reflTransGen) h
  Â· obtain (rfl | h) := reflTransGen_iff_eq_or_transGen.mp h
    -- âŠ¢ TransGen (ReflGen r) y y
    Â· exact .single .refl
      -- ğŸ‰ no goals
    Â· exact TransGen.mono (fun _ _ â†¦ .single) h
      -- ğŸ‰ no goals

@[simp] lemma reflTransGen_reflGen : ReflTransGen (ReflGen r) = ReflTransGen r := by
  simp only [â†transGen_reflGen, reflGen_eq_self reflexive_reflGen]
  -- ğŸ‰ no goals

@[simp] lemma reflTransGen_transGen : ReflTransGen (TransGen r) = ReflTransGen r := by
  simp only [â†reflGen_transGen, transGen_idem]
  -- ğŸ‰ no goals

lemma reflTransGen_eq_transGen (hr : Reflexive r) :
    ReflTransGen r = TransGen r := by
  rw [â† transGen_reflGen, reflGen_eq_self hr]
  -- ğŸ‰ no goals

lemma reflTransGen_eq_reflGen (hr : Transitive r) :
    ReflTransGen r = ReflGen r := by
  rw [â† reflGen_transGen, transGen_eq_self hr]
  -- ğŸ‰ no goals

end ReflTransGen

/-- The join of a relation on a single type is a new relation for which
pairs of terms are related if there is a third term they are both
related to.  For example, if `r` is a relation representing rewrites
in a term rewriting system, then *confluence* is the property that if
`a` rewrites to both `b` and `c`, then `join r` relates `b` and `c`
(see `Relation.church_rosser`).
-/
def Join (r : Î± â†’ Î± â†’ Prop) : Î± â†’ Î± â†’ Prop := fun a b â†¦ âˆƒ c, r a c âˆ§ r b c
#align relation.join Relation.Join

section Join

open ReflTransGen ReflGen

/-- A sufficient condition for the Church-Rosser property. -/
theorem church_rosser (h : âˆ€ a b c, r a b â†’ r a c â†’ âˆƒ d, ReflGen r b d âˆ§ ReflTransGen r c d)
    (hab : ReflTransGen r a b) (hac : ReflTransGen r a c) : Join (ReflTransGen r) b c := by
  induction hab
  -- âŠ¢ Join (ReflTransGen r) a c
  case refl => exact âŸ¨c, hac, reflâŸ©
  -- âŠ¢ Join (ReflTransGen r) câœ c
  -- ğŸ‰ no goals
  case tail d e _ hde ih =>
  rcases ih with âŸ¨b, hdb, hcbâŸ©
  have : âˆƒ a, ReflTransGen r e a âˆ§ ReflGen r b a := by
    clear hcb
    induction hdb
    case refl => exact âŸ¨e, refl, ReflGen.single hdeâŸ©
    case tail f b _ hfb ih =>
    rcases ih with âŸ¨a, hea, hfaâŸ©
    cases' hfa with _ hfa
    Â· exact âŸ¨b, hea.tail hfb, ReflGen.reflâŸ©
    Â· rcases h _ _ _ hfb hfa with âŸ¨c, hbc, hacâŸ©
      exact âŸ¨c, hea.trans hac, hbcâŸ©
  rcases this with âŸ¨a, hea, hbaâŸ©
  cases' hba with _ hba
  Â· exact âŸ¨b, hea, hcbâŸ©
  Â· exact âŸ¨a, hea, hcb.tail hbaâŸ©
#align relation.church_rosser Relation.church_rosser


theorem join_of_single (h : Reflexive r) (hab : r a b) : Join r a b :=
  âŸ¨b, hab, h bâŸ©
#align relation.join_of_single Relation.join_of_single

theorem symmetric_join : Symmetric (Join r) := fun _ _ âŸ¨c, hac, hcbâŸ© â†¦ âŸ¨c, hcb, hacâŸ©
#align relation.symmetric_join Relation.symmetric_join

theorem reflexive_join (h : Reflexive r) : Reflexive (Join r) := fun a â†¦ âŸ¨a, h a, h aâŸ©
#align relation.reflexive_join Relation.reflexive_join

theorem transitive_join (ht : Transitive r) (h : âˆ€ a b c, r a b â†’ r a c â†’ Join r b c) :
    Transitive (Join r) :=
  fun _ b _ âŸ¨x, hax, hbxâŸ© âŸ¨y, hby, hcyâŸ© â†¦
  let âŸ¨z, hxz, hyzâŸ© := h b x y hbx hby
  âŸ¨z, ht hax hxz, ht hcy hyzâŸ©
#align relation.transitive_join Relation.transitive_join

theorem equivalence_join (hr : Reflexive r) (ht : Transitive r)
  (h : âˆ€ a b c, r a b â†’ r a c â†’ Join r b c) : Equivalence (Join r) :=
  âŸ¨reflexive_join hr, @symmetric_join _ _, @transitive_join _ _ ht hâŸ©
#align relation.equivalence_join Relation.equivalence_join

theorem equivalence_join_reflTransGen
    (h : âˆ€ a b c, r a b â†’ r a c â†’ âˆƒ d, ReflGen r b d âˆ§ ReflTransGen r c d) :
    Equivalence (Join (ReflTransGen r)) :=
  equivalence_join reflexive_reflTransGen transitive_reflTransGen fun _ _ _ â†¦ church_rosser h
#align relation.equivalence_join_refl_trans_gen Relation.equivalence_join_reflTransGen

theorem join_of_equivalence {r' : Î± â†’ Î± â†’ Prop} (hr : Equivalence r) (h : âˆ€ a b, r' a b â†’ r a b) :
    Join r' a b â†’ r a b
  | âŸ¨_, hac, hbcâŸ© => hr.trans (h _ _ hac) (hr.symm <| h _ _ hbc)
#align relation.join_of_equivalence Relation.join_of_equivalence

theorem reflTransGen_of_transitive_reflexive {r' : Î± â†’ Î± â†’ Prop} (hr : Reflexive r)
    (ht : Transitive r) (h : âˆ€ a b, r' a b â†’ r a b) (h' : ReflTransGen r' a b) : r a b := by
  induction' h' with b c _ hbc ih
  -- âŠ¢ r a a
  Â· exact hr _
    -- ğŸ‰ no goals
  Â· exact ht ih (h _ _ hbc)
    -- ğŸ‰ no goals
#align relation.refl_trans_gen_of_transitive_reflexive Relation.reflTransGen_of_transitive_reflexive

theorem reflTransGen_of_equivalence {r' : Î± â†’ Î± â†’ Prop} (hr : Equivalence r) :
    (âˆ€ a b, r' a b â†’ r a b) â†’ ReflTransGen r' a b â†’ r a b :=
  reflTransGen_of_transitive_reflexive hr.1 (fun _ _ _ â†¦ hr.trans)
#align relation.refl_trans_gen_of_equivalence Relation.reflTransGen_of_equivalence

end Join

end Relation

section EqvGen

variable {r : Î± â†’ Î± â†’ Prop} {a b : Î±}

theorem Equivalence.eqvGen_iff (h : Equivalence r) : EqvGen r a b â†” r a b :=
  Iff.intro
    (by
      intro h
      -- âŠ¢ r a b
      induction h with
      | rel => assumption
      | refl => exact h.1 _
      | symm => apply h.symm; assumption
      | trans _ _ _ _ _ hab hbc => exact h.trans hab hbc)
    (EqvGen.rel a b)
#align equivalence.eqv_gen_iff Equivalence.eqvGen_iff

theorem Equivalence.eqvGen_eq (h : Equivalence r) : EqvGen r = r :=
  funext fun _ â†¦ funext fun _ â†¦ propext <| h.eqvGen_iff
#align equivalence.eqv_gen_eq Equivalence.eqvGen_eq

theorem EqvGen.mono {r p : Î± â†’ Î± â†’ Prop} (hrp : âˆ€ a b, r a b â†’ p a b) (h : EqvGen r a b) :
    EqvGen p a b := by
  induction h with
  | rel a b h => exact EqvGen.rel _ _ (hrp _ _ h)
  | refl => exact EqvGen.refl _
  | symm a b _ ih => exact EqvGen.symm _ _ ih
  | trans a b c _ _ hab hbc => exact EqvGen.trans _ _ _ hab hbc
#align eqv_gen.mono EqvGen.mono

end EqvGen
