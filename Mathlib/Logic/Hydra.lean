/-
Copyright (c) 2022 Junyan Xu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Junyan Xu
-/
import Mathlib.Data.Finsupp.Lex
import Mathlib.Data.Finsupp.Multiset
import Mathlib.Order.GameAdd

#align_import logic.hydra from "leanprover-community/mathlib"@"48085f140e684306f9e7da907cd5932056d1aded"

/-!
# Termination of a hydra game

This file deals with the following version of the hydra game: each head of the hydra is
labelled by an element in a type `Œ±`, and when you cut off one head with label `a`, it
grows back an arbitrary but finite number of heads, all labelled by elements smaller than
`a` with respect to a well-founded relation `r` on `Œ±`. We show that no matter how (in
what order) you choose cut off the heads, the game always terminates, i.e. all heads will
eventually be cut off (but of course it can last arbitrarily long, i.e. takes an
arbitrary finite number of steps).

This result is stated as the well-foundedness of the `CutExpand` relation defined in
this file: we model the heads of the hydra as a multiset of elements of `Œ±`, and the
valid "moves" of the game are modelled by the relation `CutExpand r` on `Multiset Œ±`:
`CutExpand r s' s` is true iff `s'` is obtained by removing one head `a ‚àà s` and
adding back an arbitrary multiset `t` of heads such that all `a' ‚àà t` satisfy `r a' a`.

We follow the proof by Peter LeFanu Lumsdaine at https://mathoverflow.net/a/229084/3332.

TODO: formalize the relations corresponding to more powerful (e.g. Kirby‚ÄìParis and Buchholz)
hydras, and prove their well-foundedness.
-/


namespace Relation

open Multiset Prod

variable {Œ± : Type*}

/-- The relation that specifies valid moves in our hydra game. `CutExpand r s' s`
  means that `s'` is obtained by removing one head `a ‚àà s` and adding back an arbitrary
  multiset `t` of heads such that all `a' ‚àà t` satisfy `r a' a`.

  This is most directly translated into `s' = s.erase a + t`, but `Multiset.erase` requires
  `DecidableEq Œ±`, so we use the equivalent condition `s' + {a} = s + t` instead, which
  is also easier to verify for explicit multisets `s'`, `s` and `t`.

  We also don't include the condition `a ‚àà s` because `s' + {a} = s + t` already
  guarantees `a ‚àà s + t`, and if `r` is irreflexive then `a ‚àâ t`, which is the
  case when `r` is well-founded, the case we are primarily interested in.

  The lemma `Relation.cutExpand_iff` below converts between this convenient definition
  and the direct translation when `r` is irreflexive. -/
def CutExpand (r : Œ± ‚Üí Œ± ‚Üí Prop) (s' s : Multiset Œ±) : Prop :=
  ‚àÉ (t : Multiset Œ±) (a : Œ±), (‚àÄ a' ‚àà t, r a' a) ‚àß s' + {a} = s + t
#align relation.cut_expand Relation.CutExpand

variable {r : Œ± ‚Üí Œ± ‚Üí Prop}

theorem cutExpand_le_invImage_lex [DecidableEq Œ±] [IsIrrefl Œ± r] :
    CutExpand r ‚â§ InvImage (Finsupp.Lex (r·∂ú ‚äì (¬∑ ‚â† ¬∑)) (¬∑ < ¬∑)) toFinsupp := by
  rintro s t ‚ü®u, a, hr, he‚ü©
  -- ‚ä¢ InvImage (Finsupp.Lex (r·∂ú ‚äì fun x x_1 => x ‚â† x_1) fun x x_1 => x < x_1) (‚Üëto ‚Ä¶
  replace hr := fun a' ‚Ü¶ mt (hr a')
  -- ‚ä¢ InvImage (Finsupp.Lex (r·∂ú ‚äì fun x x_1 => x ‚â† x_1) fun x x_1 => x < x_1) (‚Üëto ‚Ä¶
  classical
  refine ‚ü®a, fun b h ‚Ü¶ ?_, ?_‚ü© <;> simp_rw [toFinsupp_apply]
  ¬∑ apply_fun count b at he
    simpa only [count_add, count_singleton, if_neg h.2, add_zero, count_eq_zero.2 (hr b h.1)]
      using he
  ¬∑ apply_fun count a at he
    simp only [count_add, count_singleton_self, count_eq_zero.2 (hr _ (irrefl_of r a)),
      add_zero] at he
    exact he ‚ñ∏ Nat.lt_succ_self _
#align relation.cut_expand_le_inv_image_lex Relation.cutExpand_le_invImage_lex

theorem cutExpand_singleton {s x} (h : ‚àÄ x' ‚àà s, r x' x) : CutExpand r s {x} :=
  ‚ü®s, x, h, add_comm s _‚ü©
#align relation.cut_expand_singleton Relation.cutExpand_singleton

theorem cutExpand_singleton_singleton {x' x} (h : r x' x) : CutExpand r {x'} {x} :=
  cutExpand_singleton fun a h ‚Ü¶ by rwa [mem_singleton.1 h]
                                   -- üéâ no goals
#align relation.cut_expand_singleton_singleton Relation.cutExpand_singleton_singleton

theorem cutExpand_add_left {t u} (s) : CutExpand r (s + t) (s + u) ‚Üî CutExpand r t u :=
  exists‚ÇÇ_congr fun _ _ ‚Ü¶ and_congr Iff.rfl <| by rw [add_assoc, add_assoc, add_left_cancel_iff]
                                                  -- üéâ no goals
#align relation.cut_expand_add_left Relation.cutExpand_add_left

theorem cutExpand_iff [DecidableEq Œ±] [IsIrrefl Œ± r] {s' s : Multiset Œ±} :
    CutExpand r s' s ‚Üî
      ‚àÉ (t : Multiset Œ±) (a : Œ±), (‚àÄ a' ‚àà t, r a' a) ‚àß a ‚àà s ‚àß s' = s.erase a + t := by
  simp_rw [CutExpand, add_singleton_eq_iff]
  -- ‚ä¢ (‚àÉ t a, (‚àÄ (a' : Œ±), a' ‚àà t ‚Üí r a' a) ‚àß a ‚àà s + t ‚àß s' = erase (s + t) a) ‚Üî  ‚Ä¶
  refine' exists‚ÇÇ_congr fun t a ‚Ü¶ ‚ü®_, _‚ü©
  -- ‚ä¢ (‚àÄ (a' : Œ±), a' ‚àà t ‚Üí r a' a) ‚àß a ‚àà s + t ‚àß s' = erase (s + t) a ‚Üí (‚àÄ (a' :  ‚Ä¶
  ¬∑ rintro ‚ü®ht, ha, rfl‚ü©
    -- ‚ä¢ (‚àÄ (a' : Œ±), a' ‚àà t ‚Üí r a' a) ‚àß a ‚àà s ‚àß erase (s + t) a = erase s a + t
    obtain h | h := mem_add.1 ha
    -- ‚ä¢ (‚àÄ (a' : Œ±), a' ‚àà t ‚Üí r a' a) ‚àß a ‚àà s ‚àß erase (s + t) a = erase s a + t
    exacts [‚ü®ht, h, erase_add_left_pos t h‚ü©, (@irrefl Œ± r _ a (ht a h)).elim]
    -- üéâ no goals
  ¬∑ rintro ‚ü®ht, h, rfl‚ü©
    -- ‚ä¢ (‚àÄ (a' : Œ±), a' ‚àà t ‚Üí r a' a) ‚àß a ‚àà s + t ‚àß erase s a + t = erase (s + t) a
    exact ‚ü®ht, mem_add.2 (Or.inl h), (erase_add_left_pos t h).symm‚ü©
    -- üéâ no goals
#align relation.cut_expand_iff Relation.cutExpand_iff

theorem not_cutExpand_zero [IsIrrefl Œ± r] (s) : ¬¨CutExpand r s 0 := by
  classical
  rw [cutExpand_iff]
  rintro ‚ü®_, _, _, ‚ü®‚ü©, _‚ü©
#align relation.not_cut_expand_zero Relation.not_cutExpand_zero

/-- For any relation `r` on `Œ±`, multiset addition `Multiset Œ± √ó Multiset Œ± ‚Üí Multiset Œ±` is a
  fibration between the game sum of `CutExpand r` with itself and `CutExpand r` itself. -/
theorem cutExpand_fibration (r : Œ± ‚Üí Œ± ‚Üí Prop) :
    Fibration (GameAdd (CutExpand r) (CutExpand r)) (CutExpand r) fun s ‚Ü¶ s.1 + s.2 := by
  rintro ‚ü®s‚ÇÅ, s‚ÇÇ‚ü© s ‚ü®t, a, hr, he‚ü©; dsimp at he ‚ä¢
  -- ‚ä¢ ‚àÉ a', GameAdd (CutExpand r) (CutExpand r) a' (s‚ÇÅ, s‚ÇÇ) ‚àß (fun s => s.fst + s. ‚Ä¶
                                    -- ‚ä¢ ‚àÉ a', GameAdd (CutExpand r) (CutExpand r) a' (s‚ÇÅ, s‚ÇÇ) ‚àß a'.fst + a'.snd = s
  classical
  -- Porting note: Originally `obtain ‚ü®ha, rfl‚ü©`
  -- This is https://github.com/leanprover/std4/issues/62
  obtain ‚ü®ha, hb‚ü© := add_singleton_eq_iff.1 he
  rw [hb]
  rw [add_assoc, mem_add] at ha
  obtain h | h := ha
  ¬∑ refine' ‚ü®(s‚ÇÅ.erase a + t, s‚ÇÇ), GameAdd.fst ‚ü®t, a, hr, _‚ü©, _‚ü©
    ¬∑ rw [add_comm, ‚Üê add_assoc, singleton_add, cons_erase h]
    ¬∑ rw [add_assoc s‚ÇÅ, erase_add_left_pos _ h, add_right_comm, add_assoc]
  ¬∑ refine' ‚ü®(s‚ÇÅ, (s‚ÇÇ + t).erase a), GameAdd.snd ‚ü®t, a, hr, _‚ü©, _‚ü©
    ¬∑ rw [add_comm, singleton_add, cons_erase h]
    ¬∑ rw [add_assoc, erase_add_right_pos _ h]
#align relation.cut_expand_fibration Relation.cutExpand_fibration

/-- A multiset is accessible under `CutExpand` if all its singleton subsets are,
  assuming `r` is irreflexive. -/
theorem acc_of_singleton [IsIrrefl Œ± r] {s : Multiset Œ±} (hs : ‚àÄ a ‚àà s, Acc (CutExpand r) {a}) :
    Acc (CutExpand r) s := by
  induction s using Multiset.induction
  -- ‚ä¢ Acc (CutExpand r) 0
  case empty => exact Acc.intro 0 fun s h ‚Ü¶ (not_cutExpand_zero s h).elim
  -- ‚ä¢ Acc (CutExpand r) (a‚úù¬π ::‚Çò s‚úù)
  -- üéâ no goals
  case cons a s ihs =>
    rw [‚Üê s.singleton_add a]
    rw [forall_mem_cons] at hs
    exact (hs.1.prod_gameAdd <| ihs fun a ha ‚Ü¶ hs.2 a ha).of_fibration _ (cutExpand_fibration r)
#align relation.acc_of_singleton Relation.acc_of_singleton

/-- A singleton `{a}` is accessible under `CutExpand r` if `a` is accessible under `r`,
  assuming `r` is irreflexive. -/
theorem _root_.Acc.cutExpand [IsIrrefl Œ± r] {a : Œ±} (hacc : Acc r a) : Acc (CutExpand r) {a} := by
  induction' hacc with a h ih
  -- ‚ä¢ Acc (CutExpand r) {a}
  refine' Acc.intro _ fun s ‚Ü¶ _
  -- ‚ä¢ CutExpand r s {a} ‚Üí Acc (CutExpand r) s
  classical
  simp only [cutExpand_iff, mem_singleton]
  rintro ‚ü®t, a, hr, rfl, rfl‚ü©
  refine' acc_of_singleton fun a' ‚Ü¶ _
  rw [erase_singleton, zero_add]
  exact ih a' ‚àò hr a'
#align acc.cut_expand Acc.cutExpand

/-- `CutExpand r` is well-founded when `r` is. -/
theorem _root_.WellFounded.cutExpand (hr : WellFounded r) : WellFounded (CutExpand r) :=
  ‚ü®have := hr.isIrrefl; fun _ ‚Ü¶ acc_of_singleton fun a _ ‚Ü¶ (hr.apply a).cutExpand‚ü©
#align well_founded.cut_expand WellFounded.cutExpand

end Relation
