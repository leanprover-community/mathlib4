/-
Copyright (c) 2018 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro
-/
import Mathlib.Data.Fintype.Lattice
import Mathlib.Data.List.MinMax
import Mathlib.Data.Nat.Order.Lemmas
import Mathlib.Logic.Encodable.Basic

#align_import logic.denumerable from "leanprover-community/mathlib"@"509de852e1de55e1efa8eacfa11df0823f26f226"

/-!
# Denumerable types

This file defines denumerable (countably infinite) types as a typeclass extending `Encodable`. This
is used to provide explicit encode/decode functions from and to `‚Ñï`, with the information that those
functions are inverses of each other.

## Implementation notes

This property already has a name, namely `Œ± ‚âÉ ‚Ñï`, but here we are interested in using it as a
typeclass.
-/


variable {Œ± Œ≤ : Type*}

/-- A denumerable type is (constructively) bijective with `‚Ñï`. Typeclass equivalent of `Œ± ‚âÉ ‚Ñï`. -/
class Denumerable (Œ± : Type*) extends Encodable Œ± where
  /-- `decode` and `encode` are inverses. -/
  decode_inv : ‚àÄ n, ‚àÉ a ‚àà decode n, encode a = n
#align denumerable Denumerable

open Nat

namespace Denumerable

section

variable [Denumerable Œ±] [Denumerable Œ≤]

open Encodable

theorem decode_isSome (Œ±) [Denumerable Œ±] (n : ‚Ñï) : (decode (Œ± := Œ±) n).isSome :=
  Option.isSome_iff_exists.2 <| (decode_inv n).imp fun _ => And.left
#align denumerable.decode_is_some Denumerable.decode_isSome

/-- Returns the `n`-th element of `Œ±` indexed by the decoding. -/
def ofNat (Œ±) [Denumerable Œ±] (n : ‚Ñï) : Œ± :=
  Option.get _ (decode_isSome Œ± n)
#align denumerable.of_nat Denumerable.ofNat

@[simp]
theorem decode_eq_ofNat (Œ±) [Denumerable Œ±] (n : ‚Ñï) : decode (Œ± := Œ±) n = some (ofNat Œ± n) :=
  Option.eq_some_of_isSome _
#align denumerable.decode_eq_of_nat Denumerable.decode_eq_ofNat

@[simp]
theorem ofNat_of_decode {n b} (h : decode (Œ± := Œ±) n = some b) : ofNat (Œ± := Œ±) n = b :=
  Option.some.inj <| (decode_eq_ofNat _ _).symm.trans h
#align denumerable.of_nat_of_decode Denumerable.ofNat_of_decode

@[simp]
theorem encode_ofNat (n) : encode (ofNat Œ± n) = n := by
  obtain ‚ü®a, h, e‚ü© := decode_inv (Œ± := Œ±) n
  -- ‚ä¢ encode (ofNat Œ± n) = n
  rwa [ofNat_of_decode h]
  -- üéâ no goals
#align denumerable.encode_of_nat Denumerable.encode_ofNat

@[simp]
theorem ofNat_encode (a) : ofNat Œ± (encode a) = a :=
  ofNat_of_decode (encodek _)
#align denumerable.of_nat_encode Denumerable.ofNat_encode

/-- A denumerable type is equivalent to `‚Ñï`. -/
def eqv (Œ±) [Denumerable Œ±] : Œ± ‚âÉ ‚Ñï :=
  ‚ü®encode, ofNat Œ±, ofNat_encode, encode_ofNat‚ü©
#align denumerable.eqv Denumerable.eqv

-- See Note [lower instance priority]
instance (priority := 100) : Infinite Œ± :=
  Infinite.of_surjective _ (eqv Œ±).surjective

/-- A type equivalent to `‚Ñï` is denumerable. -/
def mk' {Œ±} (e : Œ± ‚âÉ ‚Ñï) : Denumerable Œ± where
  encode := e
  decode := some ‚àò e.symm
  encodek _ := congr_arg some (e.symm_apply_apply _)
  decode_inv _ := ‚ü®_, rfl, e.apply_symm_apply _‚ü©
#align denumerable.mk' Denumerable.mk'

/-- Denumerability is conserved by equivalences. This is transitivity of equivalence the denumerable
way. -/
def ofEquiv (Œ±) {Œ≤} [Denumerable Œ±] (e : Œ≤ ‚âÉ Œ±) : Denumerable Œ≤ :=
  { Encodable.ofEquiv _ e with
    decode_inv := fun n => by
      -- Porting note: replaced `simp`
      simp_rw [Option.mem_def, decode_ofEquiv e, encode_ofEquiv e, decode_eq_ofNat,
        Option.map_some', Option.some_inj, exists_eq_left', Equiv.apply_symm_apply,
        Denumerable.encode_ofNat] }
#align denumerable.of_equiv Denumerable.ofEquiv

@[simp]
theorem ofEquiv_ofNat (Œ±) {Œ≤} [Denumerable Œ±] (e : Œ≤ ‚âÉ Œ±) (n) :
    @ofNat Œ≤ (ofEquiv _ e) n = e.symm (ofNat Œ± n) := by
  -- Porting note: added `letI`
  letI := ofEquiv _ e
  -- ‚ä¢ ofNat Œ≤ n = ‚Üëe.symm (ofNat Œ± n)
  refine ofNat_of_decode ?_
  -- ‚ä¢ decode n = some (‚Üëe.symm (ofNat Œ± n))
  rw [decode_ofEquiv e]
  -- ‚ä¢ Option.map (‚Üëe.symm) (decode n) = some (‚Üëe.symm (ofNat Œ± n))
  simp
  -- üéâ no goals
#align denumerable.of_equiv_of_nat Denumerable.ofEquiv_ofNat

/-- All denumerable types are equivalent. -/
def equiv‚ÇÇ (Œ± Œ≤) [Denumerable Œ±] [Denumerable Œ≤] : Œ± ‚âÉ Œ≤ :=
  (eqv Œ±).trans (eqv Œ≤).symm
#align denumerable.equiv‚ÇÇ Denumerable.equiv‚ÇÇ

instance nat : Denumerable ‚Ñï :=
  ‚ü®fun _ => ‚ü®_, rfl, rfl‚ü©‚ü©
#align denumerable.nat Denumerable.nat

@[simp]
theorem ofNat_nat (n) : ofNat ‚Ñï n = n :=
  rfl
#align denumerable.of_nat_nat Denumerable.ofNat_nat

/-- If `Œ±` is denumerable, then so is `Option Œ±`. -/
instance option : Denumerable (Option Œ±) :=
  ‚ü®fun n => by
    cases n
    -- ‚ä¢ ‚àÉ a, a ‚àà decode zero ‚àß encode a = zero
    case zero =>
      refine' ‚ü®none, _, encode_none‚ü©
      rw [decode_option_zero, Option.mem_def]
    case succ n =>
      refine' ‚ü®some (ofNat Œ± n), _, _‚ü©
      ¬∑ rw [decode_option_succ, decode_eq_ofNat, Option.map_some', Option.mem_def]
      rw [encode_some, encode_ofNat]‚ü©
#align denumerable.option Denumerable.option

set_option linter.deprecated false in
/-- If `Œ±` and `Œ≤` are denumerable, then so is their sum. -/
instance sum : Denumerable (Sum Œ± Œ≤) :=
  ‚ü®fun n => by
    suffices ‚àÉ a ‚àà @decodeSum Œ± Œ≤ _ _ n, encodeSum a = bit (bodd n) (div2 n) by simpa [bit_decomp]
    -- ‚ä¢ ‚àÉ a, a ‚àà decodeSum n ‚àß encodeSum a = bit (bodd n) (div2 n)
    simp only [decodeSum, boddDiv2_eq, decode_eq_ofNat, Option.some.injEq, Option.map_some',
      Option.mem_def, Sum.exists]
    cases bodd n <;> simp [decodeSum, bit, encodeSum, bit0_eq_two_mul, bit1]‚ü©
                     -- üéâ no goals
                     -- üéâ no goals
#align denumerable.sum Denumerable.sum

section Sigma

variable {Œ≥ : Œ± ‚Üí Type*} [‚àÄ a, Denumerable (Œ≥ a)]

/-- A denumerable collection of denumerable types is denumerable. -/
instance sigma : Denumerable (Sigma Œ≥) :=
  ‚ü®fun n => by simp [decodeSigma]‚ü©
               -- üéâ no goals
#align denumerable.sigma Denumerable.sigma

@[simp]
theorem sigma_ofNat_val (n : ‚Ñï) :
    ofNat (Sigma Œ≥) n = ‚ü®ofNat Œ± (unpair n).1, ofNat (Œ≥ _) (unpair n).2‚ü© :=
  Option.some.inj <| by rw [‚Üê decode_eq_ofNat, decode_sigma_val]; simp
                        -- ‚ä¢ (Option.bind (decode (unpair n).fst) fun a => Option.map (Sigma.mk a) (decod ‚Ä¶
                                                                  -- üéâ no goals
#align denumerable.sigma_of_nat_val Denumerable.sigma_ofNat_val

end Sigma

/-- If `Œ±` and `Œ≤` are denumerable, then so is their product. -/
instance prod : Denumerable (Œ± √ó Œ≤) :=
  ofEquiv _ (Equiv.sigmaEquivProd Œ± Œ≤).symm
#align denumerable.prod Denumerable.prod

-- Porting note: removed @[simp] - simp can prove it
theorem prod_ofNat_val (n : ‚Ñï) : ofNat (Œ± √ó Œ≤) n = (ofNat Œ± (unpair n).1, ofNat Œ≤ (unpair n).2) :=
  by simp
     -- üéâ no goals
#align denumerable.prod_of_nat_val Denumerable.prod_ofNat_val

@[simp]
theorem prod_nat_ofNat : ofNat (‚Ñï √ó ‚Ñï) = unpair := by funext; simp
                                                      -- ‚ä¢ ofNat (‚Ñï √ó ‚Ñï) x‚úù = unpair x‚úù
                                                              -- üéâ no goals
#align denumerable.prod_nat_of_nat Denumerable.prod_nat_ofNat

instance int : Denumerable ‚Ñ§ :=
  Denumerable.mk' Equiv.intEquivNat
#align denumerable.int Denumerable.int

instance pnat : Denumerable ‚Ñï+ :=
  Denumerable.mk' Equiv.pnatEquivNat
#align denumerable.pnat Denumerable.pnat

/-- The lift of a denumerable type is denumerable. -/
instance ulift : Denumerable (ULift Œ±) :=
  ofEquiv _ Equiv.ulift
#align denumerable.ulift Denumerable.ulift

/-- The lift of a denumerable type is denumerable. -/
instance plift : Denumerable (PLift Œ±) :=
  ofEquiv _ Equiv.plift
#align denumerable.plift Denumerable.plift

/-- If `Œ±` is denumerable, then `Œ± √ó Œ±` and `Œ±` are equivalent. -/
def pair : Œ± √ó Œ± ‚âÉ Œ± :=
  equiv‚ÇÇ _ _
#align denumerable.pair Denumerable.pair

end

end Denumerable

namespace Nat.Subtype

open Function Encodable

/-! ### Subsets of `‚Ñï` -/


variable {s : Set ‚Ñï} [Infinite s]

section Classical

open Classical

theorem exists_succ (x : s) : ‚àÉ n, (x : ‚Ñï) + n + 1 ‚àà s :=
  _root_.by_contradiction fun h =>
    have : ‚àÄ (a : ‚Ñï) (_ : a ‚àà s), a < succ x := fun a ha =>
      lt_of_not_ge fun hax => h ‚ü®a - (x + 1), by rwa [add_right_comm, add_tsub_cancel_of_le hax]‚ü©
                                                 -- üéâ no goals
    Fintype.false
      ‚ü®(((Multiset.range (succ x)).filter (¬∑ ‚àà s)).pmap
            (fun (y : ‚Ñï) (hy : y ‚àà s) => Subtype.mk y hy)
            (by simp [-Multiset.range_succ])).toFinset,
                -- üéâ no goals
        by simpa [Subtype.ext_iff_val, Multiset.mem_filter, -Multiset.range_succ] ‚ü©
           -- üéâ no goals
#align nat.subtype.exists_succ Nat.Subtype.exists_succ

end Classical

variable [DecidablePred (¬∑ ‚àà s)]

/-- Returns the next natural in a set, according to the usual ordering of `‚Ñï`. -/
def succ (x : s) : s :=
  have h : ‚àÉ m, (x : ‚Ñï) + m + 1 ‚àà s := exists_succ x
  ‚ü®‚Üëx + Nat.find h + 1, Nat.find_spec h‚ü©
#align nat.subtype.succ Nat.Subtype.succ

theorem succ_le_of_lt {x y : s} (h : y < x) : succ y ‚â§ x :=
  have hx : ‚àÉ m, (y : ‚Ñï) + m + 1 ‚àà s := exists_succ _
  let ‚ü®k, hk‚ü© := Nat.exists_eq_add_of_lt h
  have : Nat.find hx ‚â§ k := Nat.find_min' _ (hk ‚ñ∏ x.2)
  show (y : ‚Ñï) + Nat.find hx + 1 ‚â§ x by
    rw [hk]
    -- ‚ä¢ ‚Üëy + Nat.find hx + 1 ‚â§ ‚Üëy + k + 1
    exact add_le_add_right (add_le_add_left this _) _
    -- üéâ no goals
#align nat.subtype.succ_le_of_lt Nat.Subtype.succ_le_of_lt

theorem le_succ_of_forall_lt_le {x y : s} (h : ‚àÄ z < x, z ‚â§ y) : x ‚â§ succ y :=
  have hx : ‚àÉ m, (y : ‚Ñï) + m + 1 ‚àà s := exists_succ _
  show (x : ‚Ñï) ‚â§ (y : ‚Ñï) + Nat.find hx + 1 from
    le_of_not_gt fun hxy =>
      (h ‚ü®_, Nat.find_spec hx‚ü© hxy).not_lt <|
        calc
          (y : ‚Ñï) ‚â§ (y : ‚Ñï) + Nat.find hx := le_add_of_nonneg_right (Nat.zero_le _)
          _ < (y : ‚Ñï) + Nat.find hx + 1 := Nat.lt_succ_self _
#align nat.subtype.le_succ_of_forall_lt_le Nat.Subtype.le_succ_of_forall_lt_le

theorem lt_succ_self (x : s) : x < succ x :=
  calc
    -- Porting note: replaced `x + _`, added type annotations
    (x : ‚Ñï) ‚â§ (x + Nat.find (exists_succ x): ‚Ñï) := le_self_add
    _ < (succ x : ‚Ñï) := Nat.lt_succ_self (x + _)
#align nat.subtype.lt_succ_self Nat.Subtype.lt_succ_self

theorem lt_succ_iff_le {x y : s} : x < succ y ‚Üî x ‚â§ y :=
  ‚ü®fun h => le_of_not_gt fun h' => not_le_of_gt h (succ_le_of_lt h'), fun h =>
    lt_of_le_of_lt h (lt_succ_self _)‚ü©
#align nat.subtype.lt_succ_iff_le Nat.Subtype.lt_succ_iff_le

/-- Returns the `n`-th element of a set, according to the usual ordering of `‚Ñï`. -/
def ofNat (s : Set ‚Ñï) [DecidablePred (¬∑ ‚àà s)] [Infinite s] : ‚Ñï ‚Üí s
  | 0 => ‚ä•
  | n + 1 => succ (ofNat s n)
#align nat.subtype.of_nat Nat.Subtype.ofNat

theorem ofNat_surjective_aux : ‚àÄ {x : ‚Ñï} (hx : x ‚àà s), ‚àÉ n, ofNat s n = ‚ü®x, hx‚ü©
  | x => fun hx => by
    set t : List s :=
      ((List.range x).filter fun y => y ‚àà s).pmap
        (fun (y : ‚Ñï) (hy : y ‚àà s) => ‚ü®y, hy‚ü©)
        (by intros a ha; simpa using (List.mem_filter.mp ha).2) with ht
    have hmt : ‚àÄ {y : s}, y ‚àà t ‚Üî y < ‚ü®x, hx‚ü© := by
      simp [List.mem_filter, Subtype.ext_iff_val, ht]
    have wf : ‚àÄ m : s, List.maximum t = m ‚Üí ‚Üëm < x := fun m hmax => by
      simpa using hmt.mp (List.maximum_mem hmax)
    cases' hmax : List.maximum t with m
    -- ‚ä¢ ‚àÉ n, ofNat s n = { val := x‚úù, property := hx }
    ¬∑ refine ‚ü®0, le_antisymm bot_le (le_of_not_gt fun h => List.not_mem_nil (‚ä• : s) ?_)‚ü©
      -- ‚ä¢ ‚ä• ‚àà []
      rwa [‚Üê List.maximum_eq_none.1 hmax, hmt]
      -- üéâ no goals
    cases' ofNat_surjective_aux m.2 with a ha
    -- ‚ä¢ ‚àÉ n, ofNat s n = { val := x‚úù, property := hx }
    refine ‚ü®a + 1, le_antisymm ?_ ?_‚ü© <;> rw [ofNat]
    -- ‚ä¢ ofNat s (a + 1) ‚â§ { val := x‚úù, property := hx }
                                          -- ‚ä¢ succ (ofNat s a) ‚â§ { val := x‚úù, property := hx }
                                          -- ‚ä¢ { val := x‚úù, property := hx } ‚â§ succ (ofNat s a)
    ¬∑ refine succ_le_of_lt ?_
      -- ‚ä¢ ofNat s a < { val := x‚úù, property := hx }
      rw [ha]
      -- ‚ä¢ { val := ‚Üëm, property := (_ : ‚Üëm ‚àà s) } < { val := x‚úù, property := hx }
      exact wf _ hmax
      -- üéâ no goals
    ¬∑ refine le_succ_of_forall_lt_le fun z hz => ?_
      -- ‚ä¢ z ‚â§ ofNat s a
      rw [ha]
      -- ‚ä¢ z ‚â§ { val := ‚Üëm, property := (_ : ‚Üëm ‚àà s) }
      cases m
      -- ‚ä¢ z ‚â§ { val := ‚Üë{ val := val‚úù, property := property‚úù }, property := (_ : ‚Üë{ va ‚Ä¶
      exact List.le_maximum_of_mem (hmt.2 hz) hmax
      -- üéâ no goals
decreasing_by
  tauto
  -- üéâ no goals
  -- üéâ no goals
  -- üéâ no goals
  -- üéâ no goals
  -- üéâ no goals
  -- üéâ no goals
#align nat.subtype.of_nat_surjective_aux Nat.Subtype.ofNat_surjective_aux

theorem ofNat_surjective : Surjective (ofNat s) := fun ‚ü®_, hx‚ü© => ofNat_surjective_aux hx
#align nat.subtype.of_nat_surjective Nat.Subtype.ofNat_surjective

@[simp]
theorem ofNat_range : Set.range (ofNat s) = Set.univ :=
  ofNat_surjective.range_eq
#align nat.subtype.of_nat_range Nat.Subtype.ofNat_range

@[simp]
theorem coe_comp_ofNat_range : Set.range ((‚Üë) ‚àò ofNat s : ‚Ñï ‚Üí ‚Ñï) = s := by
  rw [Set.range_comp Subtype.val, ofNat_range, Set.image_univ, Subtype.range_coe]
  -- üéâ no goals
#align nat.subtype.coe_comp_of_nat_range Nat.Subtype.coe_comp_ofNat_range

private def toFunAux (x : s) : ‚Ñï :=
  (List.range x).countP (¬∑ ‚àà s)

private theorem toFunAux_eq (x : s) : toFunAux x = ((Finset.range x).filter (¬∑ ‚àà s)).card := by
  rw [toFunAux, List.countP_eq_length_filter]
  -- ‚ä¢ List.length (List.filter (fun x => decide (x ‚àà s)) (List.range ‚Üëx)) = Finset ‚Ä¶
  rfl
  -- üéâ no goals

open Finset

private theorem right_inverse_aux : ‚àÄ n, toFunAux (ofNat s n) = n
  | 0 => by
    rw [toFunAux_eq, card_eq_zero, eq_empty_iff_forall_not_mem]
    -- ‚ä¢ ‚àÄ (x : ‚Ñï), ¬¨x ‚àà filter (fun x => x ‚àà s) (range ‚Üë(ofNat s 0))
    rintro n hn
    -- ‚ä¢ False
    rw [mem_filter, ofNat, mem_range] at hn
    -- ‚ä¢ False
    exact bot_le.not_lt (show (‚ü®n, hn.2‚ü© : s) < ‚ä• from hn.1)
    -- üéâ no goals
  | n + 1 => by
    have ih : toFunAux (ofNat s n) = n := right_inverse_aux n
    -- ‚ä¢ Nat.Subtype.toFunAux (ofNat s (n + 1)) = n + 1
    have h‚ÇÅ : (ofNat s n : ‚Ñï) ‚àâ (range (ofNat s n)).filter (¬∑ ‚àà s) := by simp
    -- ‚ä¢ Nat.Subtype.toFunAux (ofNat s (n + 1)) = n + 1
    have h‚ÇÇ : (range (succ (ofNat s n))).filter (¬∑ ‚àà s) =
        insert ‚Üë(ofNat s n) ((range (ofNat s n)).filter (¬∑ ‚àà s)) := by
      simp only [Finset.ext_iff, mem_insert, mem_range, mem_filter]
      exact fun m =>
        ‚ü®fun h => by
          simp only [h.2, and_true_iff]
          exact Or.symm (lt_or_eq_of_le ((@lt_succ_iff_le _ _ _ ‚ü®m, h.2‚ü© _).1 h.1)),
         fun h =>
          h.elim (fun h => h.symm ‚ñ∏ ‚ü®lt_succ_self _, (ofNat s n).prop‚ü©) fun h =>
            ‚ü®h.1.trans (lt_succ_self _), h.2‚ü©‚ü©
    simp only [toFunAux_eq, ofNat, range_succ] at ih ‚ä¢
    -- ‚ä¢ card (filter (fun x => x ‚àà s) (range ‚Üë(succ (ofNat s (Nat.add n 0))))) = n + 1
    conv =>
      rhs
      rw [‚Üê ih, ‚Üê card_insert_of_not_mem h‚ÇÅ, ‚Üê h‚ÇÇ]

/-- Any infinite set of naturals is denumerable. -/
def denumerable (s : Set ‚Ñï) [DecidablePred (¬∑ ‚àà s)] [Infinite s] : Denumerable s :=
  Denumerable.ofEquiv ‚Ñï
    { toFun := toFunAux
      invFun := ofNat s
      left_inv := leftInverse_of_surjective_of_rightInverse ofNat_surjective right_inverse_aux
      right_inv := right_inverse_aux }
#align nat.subtype.denumerable Nat.Subtype.denumerable

end Nat.Subtype

namespace Denumerable

open Encodable

/-- An infinite encodable type is denumerable. -/
def ofEncodableOfInfinite (Œ± : Type*) [Encodable Œ±] [Infinite Œ±] : Denumerable Œ± := by
  letI := @decidableRangeEncode Œ± _
  -- ‚ä¢ Denumerable Œ±
  letI : Infinite (Set.range (@encode Œ± _)) :=
    Infinite.of_injective _ (Equiv.ofInjective _ encode_injective).injective
  letI := Nat.Subtype.denumerable (Set.range (@encode Œ± _))
  -- ‚ä¢ Denumerable Œ±
  exact Denumerable.ofEquiv (Set.range (@encode Œ± _)) (equivRangeEncode Œ±)
  -- üéâ no goals
#align denumerable.of_encodable_of_infinite Denumerable.ofEncodableOfInfinite

end Denumerable

/-- See also `nonempty_encodable`, `nonempty_fintype`. -/
theorem nonempty_denumerable (Œ± : Type*) [Countable Œ±] [Infinite Œ±] : Nonempty (Denumerable Œ±) :=
  (nonempty_encodable Œ±).map fun h => @Denumerable.ofEncodableOfInfinite _ h _
#align nonempty_denumerable nonempty_denumerable

theorem nonempty_denumerable_iff {Œ± : Type*} :
    Nonempty (Denumerable Œ±) ‚Üî Countable Œ± ‚àß Infinite Œ± :=
  ‚ü®fun ‚ü®_‚ü© ‚Ü¶ ‚ü®inferInstance, inferInstance‚ü©, fun ‚ü®_, _‚ü© ‚Ü¶ nonempty_denumerable _‚ü©

instance nonempty_equiv_of_countable [Countable Œ±] [Infinite Œ±] [Countable Œ≤] [Infinite Œ≤] :
    Nonempty (Œ± ‚âÉ Œ≤) := by
  cases nonempty_denumerable Œ±
  -- ‚ä¢ Nonempty (Œ± ‚âÉ Œ≤)
  cases nonempty_denumerable Œ≤
  -- ‚ä¢ Nonempty (Œ± ‚âÉ Œ≤)
  exact ‚ü®(Denumerable.eqv _).trans (Denumerable.eqv _).symm‚ü©
  -- üéâ no goals
#align nonempty_equiv_of_countable nonempty_equiv_of_countable
