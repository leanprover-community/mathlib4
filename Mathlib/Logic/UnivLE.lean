/-
Copyright (c) 2023 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Logic.Small.Basic

/-!
# UnivLE

A proposition expressing a universe inequality. `UnivLE.{u, v}` expresses that `u â‰¤ v`,
in the form `âˆ€ Î± : Type max u v, Small.{v} Î±`.

See the doc-string for the comparison with an alternative weaker definition.

See also `Mathlib.CategoryTheory.UnivLE` for the statement
`UnivLE.{u,v} â†” EssSurj (uliftFunctor : Type v â¥¤ Type max u v)`.
-/

set_option autoImplicit true

noncomputable section

/--
A class expressing a universe inequality. `UnivLE.{u, v}` expresses that `u â‰¤ v`.

There are (at least) two plausible definitions for `u â‰¤ v`:
* strong: `âˆ€ Î± : Type max u v, Small.{v} Î±`
* weak: `âˆ€ Î± : Type u, Small.{v} Î±`

The weak definition has the advantage of being transitive.
However only under the strong definition do we have `Small.{v} ((Î± : Type u) â†’ (Î² : Type v))`,
which is essential for proving that `Type v` has `Type u`-indexed limits when `u â‰¤ v`.

The strong definition implies the weaker definition (see below),
but we can not prove the reverse implication.
This is because in Lean's type theory, while `max u v` is at least at big as `u` and `v`,
it could be bigger than both!
-/
@[pp_with_univ]
abbrev UnivLE.{u, v} : Prop := âˆ€ Î± : Type max u v, Small.{v} Î±

example : UnivLE.{u, u} := inferInstance
example : UnivLE.{u, u+1} := inferInstance
example : UnivLE.{0, u} := inferInstance
example : UnivLE.{u, max u v} := inferInstance

instance [UnivLE.{u, v}] (Î± : Type u) : Small.{v} Î± :=
  âŸ¨Shrink.{v, max u v} (ULift.{v} Î±),
    âŸ¨Equiv.ulift.symm.trans (equivShrink (ULift Î±))âŸ©âŸ©

example : Â¬ UnivLE.{u+1, u} := by
  simp only [Small_iff, not_forall, not_exists, not_nonempty_iff]
  -- âŠ¢ âˆƒ x, âˆ€ (x_1 : Type u), IsEmpty (x â‰ƒ x_1)
  exact âŸ¨Type u, fun Î± => âŸ¨fun f => Function.not_surjective_Type.{u, u} f.symm f.symm.surjectiveâŸ©âŸ©
  -- ğŸ‰ no goals
