/-
Copyright (c) 2015 Microsoft Corporation. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Leonardo de Moura, Mario Carneiro
-/
import Mathlib.Logic.Equiv.Nat
import Mathlib.Data.PNat.Basic
import Mathlib.Order.Directed
import Mathlib.Data.Countable.Defs
import Mathlib.Order.RelIso.Basic
import Mathlib.Data.Fin.Basic

#align_import logic.encodable.basic from "leanprover-community/mathlib"@"7c523cb78f4153682c2929e3006c863bfef463d0"

/-!
# Encodable types

This file defines encodable (constructively countable) types as a typeclass.
This is used to provide explicit encode/decode functions from and to `‚Ñï`, with the information that
those functions are inverses of each other.
The difference with `Denumerable` is that finite types are encodable. For infinite types,
`Encodable` and `Denumerable` agree.

## Main declarations

* `Encodable Œ±`: States that there exists an explicit encoding function `encode : Œ± ‚Üí ‚Ñï` with a
  partial inverse `decode : ‚Ñï ‚Üí Option Œ±`.
* `decode‚ÇÇ`: Version of `decode` that is equal to `none` outside of the range of `encode`. Useful as
  we do not require this in the definition of `decode`.
* `ULower Œ±`: Any encodable type has an equivalent type living in the lowest universe, namely a
  subtype of `‚Ñï`. `ULower Œ±` finds it.

## Implementation notes

The point of asking for an explicit partial inverse `decode : ‚Ñï ‚Üí Option Œ±` to `encode : Œ± ‚Üí ‚Ñï` is
to make the range of `encode` decidable even when the finiteness of `Œ±` is not.
-/


open Option List Nat Function

/-- Constructively countable type. Made from an explicit injection `encode : Œ± ‚Üí ‚Ñï` and a partial
inverse `decode : ‚Ñï ‚Üí Option Œ±`. Note that finite types *are* countable. See `Denumerable` if you
wish to enforce infiniteness. -/
class Encodable (Œ± : Type*) where
  /-- Encoding from Type Œ± to ‚Ñï -/
  encode : Œ± ‚Üí ‚Ñï
  --Porting note: was `decode [] : ‚Ñï ‚Üí Option Œ±`. This means that `decode` does not take the type
  --explicitly in Lean4
  /-- Decoding from ‚Ñï to Option Œ±-/
  decode : ‚Ñï ‚Üí Option Œ±
  /-- Invariant relationship between encoding and decoding-/
  encodek : ‚àÄ a, decode (encode a) = some a
#align encodable Encodable

attribute [simp] Encodable.encodek

namespace Encodable

variable {Œ± : Type*} {Œ≤ : Type*}

universe u

theorem encode_injective [Encodable Œ±] : Function.Injective (@encode Œ± _)
  | x, y, e => Option.some.inj <| by rw [‚Üê encodek, e, encodek]
                                     -- üéâ no goals
#align encodable.encode_injective Encodable.encode_injective

@[simp]
theorem encode_inj [Encodable Œ±] {a b : Œ±} : encode a = encode b ‚Üî a = b :=
  encode_injective.eq_iff
#align encodable.encode_inj Encodable.encode_inj

-- The priority of the instance below is less than the priorities of `Subtype.Countable`
-- and `Quotient.Countable`
instance (priority := 400) countable [Encodable Œ±] : Countable Œ± where
  exists_injective_nat' := ‚ü®_,encode_injective‚ü©

theorem surjective_decode_iget (Œ± : Type*) [Encodable Œ±] [Inhabited Œ±] :
    Surjective fun n => ((Encodable.decode n).iget : Œ±) := fun x =>
  ‚ü®Encodable.encode x, by simp_rw [Encodable.encodek]‚ü©
                          -- üéâ no goals
#align encodable.surjective_decode_iget Encodable.surjective_decode_iget

/-- An encodable type has decidable equality. Not set as an instance because this is usually not the
best way to infer decidability. -/
def decidableEqOfEncodable (Œ±) [Encodable Œ±] : DecidableEq Œ±
  | _, _ => decidable_of_iff _ encode_inj
#align encodable.decidable_eq_of_encodable Encodable.decidableEqOfEncodable

/-- If `Œ±` is encodable and there is an injection `f : Œ≤ ‚Üí Œ±`, then `Œ≤` is encodable as well. -/
def ofLeftInjection [Encodable Œ±] (f : Œ≤ ‚Üí Œ±) (finv : Œ± ‚Üí Option Œ≤)
    (linv : ‚àÄ b, finv (f b) = some b) : Encodable Œ≤ :=
  ‚ü®fun b => encode (f b), fun n => (decode n).bind finv, fun b => by
    simp [Encodable.encodek, linv]‚ü©
    -- üéâ no goals
#align encodable.of_left_injection Encodable.ofLeftInjection

/-- If `Œ±` is encodable and `f : Œ≤ ‚Üí Œ±` is invertible, then `Œ≤` is encodable as well. -/
def ofLeftInverse [Encodable Œ±] (f : Œ≤ ‚Üí Œ±) (finv : Œ± ‚Üí Œ≤) (linv : ‚àÄ b, finv (f b) = b) :
    Encodable Œ≤ :=
  ofLeftInjection f (some ‚àò finv) fun b => congr_arg some (linv b)
#align encodable.of_left_inverse Encodable.ofLeftInverse

/-- Encodability is preserved by equivalence. -/
def ofEquiv (Œ±) [Encodable Œ±] (e : Œ≤ ‚âÉ Œ±) : Encodable Œ≤ :=
  ofLeftInverse e e.symm e.left_inv
#align encodable.of_equiv Encodable.ofEquiv

-- Porting note: removing @[simp], too powerful
theorem encode_ofEquiv {Œ± Œ≤} [Encodable Œ±] (e : Œ≤ ‚âÉ Œ±) (b : Œ≤) :
    @encode _ (ofEquiv _ e) b = encode (e b) :=
  rfl
#align encodable.encode_of_equiv Encodable.encode_ofEquiv

-- Porting note: removing @[simp], too powerful
theorem decode_ofEquiv {Œ± Œ≤} [Encodable Œ±] (e : Œ≤ ‚âÉ Œ±) (n : ‚Ñï) :
    @decode _ (ofEquiv _ e) n = (decode n).map e.symm :=
  show Option.bind _ _ = Option.map _ _
  by rw [Option.map_eq_bind]
     -- üéâ no goals
#align encodable.decode_of_equiv Encodable.decode_ofEquiv

instance _root_.Nat.encodable : Encodable ‚Ñï :=
  ‚ü®id, some, fun _ => rfl‚ü©
#align nat.encodable Nat.encodable

@[simp]
theorem encode_nat (n : ‚Ñï) : encode n = n :=
  rfl
#align encodable.encode_nat Encodable.encode_nat

@[simp 1100]
theorem decode_nat (n : ‚Ñï) : decode n = some n :=
  rfl
#align encodable.decode_nat Encodable.decode_nat

instance (priority := 100) _root_.IsEmpty.toEncodable [IsEmpty Œ±] : Encodable Œ± :=
  ‚ü®isEmptyElim, fun _ => none, isEmptyElim‚ü©
#align is_empty.to_encodable IsEmpty.toEncodable

instance _root_.PUnit.encodable : Encodable PUnit :=
  ‚ü®fun _ => 0, fun n => Nat.casesOn n (some PUnit.unit) fun _ => none, fun _ => by simp‚ü©
                                                                                   -- üéâ no goals
#align punit.encodable PUnit.encodable

@[simp]
theorem encode_star : encode PUnit.unit = 0 :=
  rfl
#align encodable.encode_star Encodable.encode_star

@[simp]
theorem decode_unit_zero : decode 0 = some PUnit.unit :=
  rfl
#align encodable.decode_unit_zero Encodable.decode_unit_zero

@[simp]
theorem decode_unit_succ (n) : decode (succ n) = (none : Option PUnit) :=
  rfl
#align encodable.decode_unit_succ Encodable.decode_unit_succ

/-- If `Œ±` is encodable, then so is `Option Œ±`. -/
instance _root_.Option.encodable {Œ± : Type*} [h : Encodable Œ±] : Encodable (Option Œ±) :=
  ‚ü®fun o => Option.casesOn o Nat.zero fun a => succ (encode a), fun n =>
    Nat.casesOn n (some none) fun m => (decode m).map some, fun o => by
    cases o <;> dsimp; simp [encodek, Nat.succ_ne_zero]‚ü©
    -- ‚ä¢ (fun n => Nat.casesOn n (some none) fun m => Option.map some (decode m)) ((f ‚Ä¶
                -- üéâ no goals
                -- ‚ä¢ Option.map some (decode (encode val‚úù)) = some (some val‚úù)
                       -- üéâ no goals
#align option.encodable Option.encodable

@[simp]
theorem encode_none [Encodable Œ±] : encode (@none Œ±) = 0 :=
  rfl
#align encodable.encode_none Encodable.encode_none

@[simp]
theorem encode_some [Encodable Œ±] (a : Œ±) : encode (some a) = succ (encode a) :=
  rfl
#align encodable.encode_some Encodable.encode_some

@[simp]
theorem decode_option_zero [Encodable Œ±] : (decode 0 : Option (Option Œ±))= some none :=
  rfl
#align encodable.decode_option_zero Encodable.decode_option_zero

@[simp]
theorem decode_option_succ [Encodable Œ±] (n) :
    (decode (succ n) : Option (Option Œ±)) = (decode n).map some :=
  rfl
#align encodable.decode_option_succ Encodable.decode_option_succ

/-- Failsafe variant of `decode`. `decode‚ÇÇ Œ± n` returns the preimage of `n` under `encode` if it
exists, and returns `none` if it doesn't. This requirement could be imposed directly on `decode` but
is not to help make the definition easier to use. -/
def decode‚ÇÇ (Œ±) [Encodable Œ±] (n : ‚Ñï) : Option Œ± :=
  (decode n).bind (Option.guard fun a => encode a = n)
#align encodable.decode‚ÇÇ Encodable.decode‚ÇÇ

theorem mem_decode‚ÇÇ' [Encodable Œ±] {n : ‚Ñï} {a : Œ±} :
    a ‚àà decode‚ÇÇ Œ± n ‚Üî a ‚àà decode n ‚àß encode a = n := by
  simp [decode‚ÇÇ]; exact ‚ü®fun ‚ü®_, h‚ÇÅ, rfl, h‚ÇÇ‚ü© => ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©, fun ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© => ‚ü®_, h‚ÇÅ, rfl, h‚ÇÇ‚ü©‚ü©
  -- ‚ä¢ (‚àÉ a_1, decode n = some a_1 ‚àß a_1 = a ‚àß encode a_1 = n) ‚Üî decode n = some a  ‚Ä¶
                  -- üéâ no goals
#align encodable.mem_decode‚ÇÇ' Encodable.mem_decode‚ÇÇ'

theorem mem_decode‚ÇÇ [Encodable Œ±] {n : ‚Ñï} {a : Œ±} : a ‚àà decode‚ÇÇ Œ± n ‚Üî encode a = n :=
  mem_decode‚ÇÇ'.trans (and_iff_right_of_imp fun e => e ‚ñ∏ encodek _)
#align encodable.mem_decode‚ÇÇ Encodable.mem_decode‚ÇÇ

theorem decode‚ÇÇ_eq_some [Encodable Œ±] {n : ‚Ñï} {a : Œ±} : decode‚ÇÇ Œ± n = some a ‚Üî encode a = n :=
  mem_decode‚ÇÇ
#align encodable.decode‚ÇÇ_eq_some Encodable.decode‚ÇÇ_eq_some

@[simp]
theorem decode‚ÇÇ_encode [Encodable Œ±] (a : Œ±) : decode‚ÇÇ Œ± (encode a) = some a := by
  ext
  -- ‚ä¢ a‚úù ‚àà decode‚ÇÇ Œ± (encode a) ‚Üî a‚úù ‚àà some a
  simp [mem_decode‚ÇÇ, eq_comm, decode‚ÇÇ_eq_some]
  -- üéâ no goals
#align encodable.decode‚ÇÇ_encode Encodable.decode‚ÇÇ_encode

theorem decode‚ÇÇ_ne_none_iff [Encodable Œ±] {n : ‚Ñï} :
    decode‚ÇÇ Œ± n ‚â† none ‚Üî n ‚àà Set.range (encode : Œ± ‚Üí ‚Ñï) := by
  simp_rw [Set.range, Set.mem_setOf_eq, Ne.def, Option.eq_none_iff_forall_not_mem,
    Encodable.mem_decode‚ÇÇ, not_forall, not_not]
#align encodable.decode‚ÇÇ_ne_none_iff Encodable.decode‚ÇÇ_ne_none_iff

theorem decode‚ÇÇ_is_partial_inv [Encodable Œ±] : IsPartialInv encode (decode‚ÇÇ Œ±) := fun _ _ =>
  mem_decode‚ÇÇ
#align encodable.decode‚ÇÇ_is_partial_inv Encodable.decode‚ÇÇ_is_partial_inv

theorem decode‚ÇÇ_inj [Encodable Œ±] {n : ‚Ñï} {a‚ÇÅ a‚ÇÇ : Œ±} (h‚ÇÅ : a‚ÇÅ ‚àà decode‚ÇÇ Œ± n)
    (h‚ÇÇ : a‚ÇÇ ‚àà decode‚ÇÇ Œ± n) : a‚ÇÅ = a‚ÇÇ :=
  encode_injective <| (mem_decode‚ÇÇ.1 h‚ÇÅ).trans (mem_decode‚ÇÇ.1 h‚ÇÇ).symm
#align encodable.decode‚ÇÇ_inj Encodable.decode‚ÇÇ_inj

theorem encodek‚ÇÇ [Encodable Œ±] (a : Œ±) : decode‚ÇÇ Œ± (encode a) = some a :=
  mem_decode‚ÇÇ.2 rfl
#align encodable.encodek‚ÇÇ Encodable.encodek‚ÇÇ

/-- The encoding function has decidable range. -/
def decidableRangeEncode (Œ± : Type*) [Encodable Œ±] : DecidablePred (¬∑ ‚àà Set.range (@encode Œ± _)) :=
  fun x =>
  decidable_of_iff (Option.isSome (decode‚ÇÇ Œ± x))
    ‚ü®fun h => ‚ü®Option.get _ h, by rw [‚Üê decode‚ÇÇ_is_partial_inv (Option.get _ h), Option.some_get]‚ü©,
                                  -- üéâ no goals
      fun ‚ü®n, hn‚ü© => by rw [‚Üê hn, encodek‚ÇÇ]; exact rfl‚ü©
                        -- ‚ä¢ isSome (some n) = true
                                             -- üéâ no goals
#align encodable.decidable_range_encode Encodable.decidableRangeEncode

/-- An encodable type is equivalent to the range of its encoding function. -/
def equivRangeEncode (Œ± : Type*) [Encodable Œ±] : Œ± ‚âÉ Set.range (@encode Œ± _)
    where
  toFun := fun a : Œ± => ‚ü®encode a, Set.mem_range_self _‚ü©
  invFun n :=
    Option.get _
      (show isSome (decode‚ÇÇ Œ± n.1) by cases' n.2 with x hx; rw [‚Üê hx, encodek‚ÇÇ]; exact rfl)
                                      -- ‚ä¢ isSome (decode‚ÇÇ Œ± ‚Üën) = true
                                                            -- ‚ä¢ isSome (some x) = true
                                                                                 -- üéâ no goals
  left_inv a := by dsimp; rw [‚Üê Option.some_inj, Option.some_get, encodek‚ÇÇ]
                   -- ‚ä¢ Option.get (decode‚ÇÇ Œ± (encode a)) (_ : isSome (decode‚ÇÇ Œ± (encode a)) = true) ‚Ä¶
                          -- üéâ no goals
  right_inv := fun ‚ü®n, x, hx‚ü© => by
    apply Subtype.eq
    -- ‚ä¢ ‚Üë((fun a => { val := encode a, property := (_ : encode a ‚àà Set.range encode) ‚Ä¶
    dsimp
    -- ‚ä¢ encode (Option.get (decode‚ÇÇ Œ± n) (_ : isSome (decode‚ÇÇ Œ± n) = true)) = n
    conv =>
      rhs
      rw [‚Üê hx]
    rw [encode_injective.eq_iff, ‚Üê Option.some_inj, Option.some_get, ‚Üê hx, encodek‚ÇÇ]
    -- üéâ no goals
#align encodable.equiv_range_encode Encodable.equivRangeEncode

/-- A type with unique element is encodable. This is not an instance to avoid diamonds. -/
def _root_.Unique.encodable [Unique Œ±] : Encodable Œ± :=
  ‚ü®fun _ => 0, fun _ => some default, Unique.forall_iff.2 rfl‚ü©
#align unique.encodable Unique.encodable

section Sum

variable [Encodable Œ±] [Encodable Œ≤]

--Porting note: removing bit0 and bit1
/-- Explicit encoding function for the sum of two encodable types. -/
def encodeSum : Sum Œ± Œ≤ ‚Üí ‚Ñï
  | Sum.inl a => 2 * encode a
  | Sum.inr b => 2 * encode b + 1
#align encodable.encode_sum Encodable.encodeSum

/-- Explicit decoding function for the sum of two encodable types. -/
def decodeSum (n : ‚Ñï) : Option (Sum Œ± Œ≤) :=
  match boddDiv2 n with
  | (false, m) => (decode m : Option Œ±).map Sum.inl
  | (_, m) => (decode m : Option Œ≤).map Sum.inr
#align encodable.decode_sum Encodable.decodeSum

/-- If `Œ±` and `Œ≤` are encodable, then so is their sum. -/
instance _root_.Sum.encodable : Encodable (Sum Œ± Œ≤) :=
  ‚ü®encodeSum, decodeSum, fun s => by cases s <;> simp [encodeSum, div2_val, decodeSum, encodek]‚ü©
                                     -- ‚ä¢ decodeSum (encodeSum (Sum.inl val‚úù)) = some (Sum.inl val‚úù)
                                                 -- üéâ no goals
                                                 -- üéâ no goals
#align sum.encodable Sum.encodable

--Porting note: removing bit0 and bit1 from statement
@[simp]
theorem encode_inl (a : Œ±) : @encode (Sum Œ± Œ≤) _ (Sum.inl a) = 2 * (encode a) :=
  rfl
#align encodable.encode_inl Encodable.encode_inl‚Çì

--Porting note: removing bit0 and bit1 from statement
@[simp]
theorem encode_inr (b : Œ≤) : @encode (Sum Œ± Œ≤) _ (Sum.inr b) = 2 * (encode b) + 1 :=
  rfl
#align encodable.encode_inr Encodable.encode_inr‚Çì

@[simp]
theorem decode_sum_val (n : ‚Ñï) : (decode n : Option (Sum Œ± Œ≤)) = decodeSum n :=
  rfl
#align encodable.decode_sum_val Encodable.decode_sum_val

end Sum

instance _root_.Bool.encodable : Encodable Bool :=
  ofEquiv (Sum Unit Unit) Equiv.boolEquivPUnitSumPUnit
#align bool.encodable Bool.encodable

@[simp]
theorem encode_true : encode true = 1 :=
  rfl
#align encodable.encode_tt Encodable.encode_true

@[simp]
theorem encode_false : encode false = 0 :=
  rfl
#align encodable.encode_ff Encodable.encode_false

@[simp]
theorem decode_zero : (decode 0 : Option Bool) = some false :=
  rfl
#align encodable.decode_zero Encodable.decode_zero

@[simp]
theorem decode_one : (decode 1 : Option Bool) = some true :=
  rfl
#align encodable.decode_one Encodable.decode_one

theorem decode_ge_two (n) (h : 2 ‚â§ n) : (decode n : Option Bool) = none := by
  suffices decodeSum n = none by
    change (decodeSum n).bind _ = none
    rw [this]
    rfl
  have : 1 ‚â§ n / 2 := by
    rw [Nat.le_div_iff_mul_le]
    exacts [h, by decide]
  cases' exists_eq_succ_of_ne_zero (_root_.ne_of_gt this) with m e
  -- ‚ä¢ decodeSum n = none
  simp [decodeSum, div2_val]; cases bodd n <;> simp [e]
  -- ‚ä¢ (match (bodd n, n / 2) with
                                               -- üéâ no goals
                                               -- üéâ no goals
#align encodable.decode_ge_two Encodable.decode_ge_two

noncomputable instance _root_.Prop.encodable : Encodable Prop :=
  ofEquiv Bool Equiv.propEquivBool
#align Prop.encodable Prop.encodable

section Sigma

variable {Œ≥ : Œ± ‚Üí Type*} [Encodable Œ±] [‚àÄ a, Encodable (Œ≥ a)]

/-- Explicit encoding function for `Sigma Œ≥` -/
def encodeSigma : Sigma Œ≥ ‚Üí ‚Ñï
  | ‚ü®a, b‚ü© => pair (encode a) (encode b)
#align encodable.encode_sigma Encodable.encodeSigma

/-- Explicit decoding function for `Sigma Œ≥` -/
def decodeSigma (n : ‚Ñï) : Option (Sigma Œ≥) :=
  let (n‚ÇÅ, n‚ÇÇ) := unpair n
  (decode n‚ÇÅ).bind fun a => (decode n‚ÇÇ).map <| Sigma.mk a
#align encodable.decode_sigma Encodable.decodeSigma

instance _root_.Sigma.encodable : Encodable (Sigma Œ≥) :=
  ‚ü®encodeSigma, decodeSigma, fun ‚ü®a, b‚ü© => by
    simp [encodeSigma, decodeSigma, unpair_pair, encodek]‚ü©
    -- üéâ no goals
#align sigma.encodable Sigma.encodable

@[simp]
theorem decode_sigma_val (n : ‚Ñï) :
    (decode n : Option (Sigma Œ≥)) =
      (decode n.unpair.1).bind fun a => (decode n.unpair.2).map <| Sigma.mk a :=
  rfl
#align encodable.decode_sigma_val Encodable.decode_sigma_val

@[simp]
theorem encode_sigma_val (a b) : @encode (Sigma Œ≥) _ ‚ü®a, b‚ü© = pair (encode a) (encode b) :=
  rfl
#align encodable.encode_sigma_val Encodable.encode_sigma_val

end Sigma

section Prod

variable [Encodable Œ±] [Encodable Œ≤]

/-- If `Œ±` and `Œ≤` are encodable, then so is their product. -/
instance Prod.encodable : Encodable (Œ± √ó Œ≤) :=
  ofEquiv _ (Equiv.sigmaEquivProd Œ± Œ≤).symm

@[simp]
theorem decode_prod_val [i : Encodable Œ±] (n : ‚Ñï) :
    (@decode (Œ± √ó Œ≤) _ n : Option (Œ± √ó Œ≤))
      = (decode n.unpair.1).bind fun a => (decode n.unpair.2).map <| Prod.mk a := by
  simp only [decode_ofEquiv, Equiv.symm_symm, decode_sigma_val]
  -- ‚ä¢ Option.map (‚Üë(Equiv.sigmaEquivProd Œ± Œ≤)) (Option.bind (decode (unpair n).fst ‚Ä¶
  cases (decode n.unpair.1 : Option Œ±) <;> cases (decode n.unpair.2 : Option Œ≤)
  -- ‚ä¢ Option.map (‚Üë(Equiv.sigmaEquivProd Œ± Œ≤)) (Option.bind none fun a => Option.m ‚Ä¶
                                           -- ‚ä¢ Option.map (‚Üë(Equiv.sigmaEquivProd Œ± Œ≤)) (Option.bind none fun a => Option.m ‚Ä¶
                                           -- ‚ä¢ Option.map (‚Üë(Equiv.sigmaEquivProd Œ± Œ≤)) (Option.bind (some val‚úù) fun a => O ‚Ä¶
  <;> rfl
      -- üéâ no goals
      -- üéâ no goals
      -- üéâ no goals
      -- üéâ no goals
#align encodable.decode_prod_val Encodable.decode_prod_val

@[simp]
theorem encode_prod_val (a b) : @encode (Œ± √ó Œ≤) _ (a, b) = pair (encode a) (encode b) :=
  rfl
#align encodable.encode_prod_val Encodable.encode_prod_val

end Prod

section Subtype

open Subtype Decidable

variable {P : Œ± ‚Üí Prop} [encA : Encodable Œ±] [decP : DecidablePred P]

--include encA

/-- Explicit encoding function for a decidable subtype of an encodable type -/
def encodeSubtype : { a : Œ± // P a } ‚Üí ‚Ñï
  | ‚ü®v,_‚ü© => encode v
#align encodable.encode_subtype Encodable.encodeSubtype

--include decP

/-- Explicit decoding function for a decidable subtype of an encodable type -/
def decodeSubtype (v : ‚Ñï) : Option { a : Œ± // P a } :=
  (decode v).bind fun a => if h : P a then some ‚ü®a, h‚ü© else none
#align encodable.decode_subtype Encodable.decodeSubtype

/-- A decidable subtype of an encodable type is encodable. -/
instance _root_.Subtype.encodable : Encodable { a : Œ± // P a } :=
  ‚ü®encodeSubtype, decodeSubtype, fun ‚ü®v, h‚ü© => by simp [encodeSubtype, decodeSubtype, encodek, h]‚ü©
                                                  -- üéâ no goals
#align subtype.encodable Subtype.encodable

theorem Subtype.encode_eq (a : Subtype P) : encode a = encode a.val := by cases a; rfl
                                                                          -- ‚ä¢ encode { val := val‚úù, property := property‚úù } = encode ‚Üë{ val := val‚úù, prope ‚Ä¶
                                                                                   -- üéâ no goals
#align encodable.subtype.encode_eq Encodable.Subtype.encode_eq

end Subtype

instance _root_.Fin.encodable (n) : Encodable (Fin n) :=
  ofEquiv _ Fin.equivSubtype
#align fin.encodable Fin.encodable

instance _root_.Int.encodable : Encodable ‚Ñ§ :=
  ofEquiv _ Equiv.intEquivNat
#align int.encodable Int.encodable

instance _root_.PNat.encodable : Encodable ‚Ñï+ :=
  ofEquiv _ Equiv.pnatEquivNat
#align pnat.encodable PNat.encodable

/-- The lift of an encodable type is encodable -/
instance _root_.ULift.encodable [Encodable Œ±] : Encodable (ULift Œ±) :=
  ofEquiv _ Equiv.ulift
#align ulift.encodable ULift.encodable

/-- The lift of an encodable type is encodable. -/
instance _root_.PLift.encodable [Encodable Œ±] : Encodable (PLift Œ±) :=
  ofEquiv _ Equiv.plift
#align plift.encodable PLift.encodable

/-- If `Œ≤` is encodable and there is an injection `f : Œ± ‚Üí Œ≤`, then `Œ±` is encodable as well. -/
noncomputable def ofInj [Encodable Œ≤] (f : Œ± ‚Üí Œ≤) (hf : Injective f) : Encodable Œ± :=
  ofLeftInjection f (partialInv f) fun _ => (partialInv_of_injective hf _ _).2 rfl
#align encodable.of_inj Encodable.ofInj

/-- If `Œ±` is countable, then it has a (non-canonical) `Encodable` structure. -/
noncomputable def ofCountable (Œ± : Type*) [Countable Œ±] : Encodable Œ± :=
  Nonempty.some <|
    let ‚ü®f, hf‚ü© := exists_injective_nat Œ±
    ‚ü®ofInj f hf‚ü©
#align encodable.of_countable Encodable.ofCountable

@[simp]
theorem nonempty_encodable : Nonempty (Encodable Œ±) ‚Üî Countable Œ± :=
  ‚ü®fun ‚ü®h‚ü© => @Encodable.countable Œ± h, fun h => ‚ü®@ofCountable _ h‚ü©‚ü©
#align encodable.nonempty_encodable Encodable.nonempty_encodable

end Encodable

/-- See also `nonempty_fintype`, `nonempty_denumerable`. -/
theorem nonempty_encodable (Œ± : Type*) [Countable Œ±] : Nonempty (Encodable Œ±) :=
  ‚ü®Encodable.ofCountable _‚ü©
#align nonempty_encodable nonempty_encodable

instance : Countable ‚Ñï+ := by delta PNat; infer_instance
                              -- ‚ä¢ Countable { n // 0 < n }
                                          -- üéâ no goals

-- short-circuit instance search
section ULower

attribute [local instance] Encodable.decidableRangeEncode

/-- `ULower Œ± : Type` is an equivalent type in the lowest universe, given `Encodable Œ±`. -/
def ULower (Œ± : Type*) [Encodable Œ±] : Type :=
  Set.range (Encodable.encode : Œ± ‚Üí ‚Ñï)
#align ulower ULower

instance {Œ± : Type*} [Encodable Œ±] : DecidableEq (ULower Œ±) :=
  by delta ULower; exact Encodable.decidableEqOfEncodable _
     -- ‚ä¢ DecidableEq ‚Üë(Set.range Encodable.encode)
                   -- üéâ no goals

instance {Œ± : Type*} [Encodable Œ±] : Encodable (ULower Œ±) :=
  by delta ULower; infer_instance
     -- ‚ä¢ Encodable ‚Üë(Set.range Encodable.encode)
                   -- üéâ no goals

end ULower

namespace ULower

variable (Œ± : Type*) [Encodable Œ±]

/-- The equivalence between the encodable type `Œ±` and `ULower Œ± : Type`. -/
def equiv : Œ± ‚âÉ ULower Œ± :=
  Encodable.equivRangeEncode Œ±
#align ulower.equiv ULower.equiv

variable {Œ±}

/-- Lowers an `a : Œ±` into `ULower Œ±`. -/
def down (a : Œ±) : ULower Œ± :=
  equiv Œ± a
#align ulower.down ULower.down

instance [Inhabited Œ±] : Inhabited (ULower Œ±) :=
  ‚ü®down default‚ü©

/-- Lifts an `a : ULower Œ±` into `Œ±`. -/
def up (a : ULower Œ±) : Œ± :=
  (equiv Œ±).symm a
#align ulower.up ULower.up

@[simp]
theorem down_up {a : ULower Œ±} : down a.up = a :=
  Equiv.right_inv _ _
#align ulower.down_up ULower.down_up

@[simp]
theorem up_down {a : Œ±} : (down a).up = a := by
  simp [up, down,Equiv.left_inv _ _, Equiv.symm_apply_apply]
  -- üéâ no goals
#align ulower.up_down ULower.up_down

@[simp]
theorem up_eq_up {a b : ULower Œ±} : a.up = b.up ‚Üî a = b :=
  Equiv.apply_eq_iff_eq _
#align ulower.up_eq_up ULower.up_eq_up

@[simp]
theorem down_eq_down {a b : Œ±} : down a = down b ‚Üî a = b :=
  Equiv.apply_eq_iff_eq _
#align ulower.down_eq_down ULower.down_eq_down

@[ext]
protected theorem ext {a b : ULower Œ±} : a.up = b.up ‚Üí a = b :=
  up_eq_up.1
#align ulower.ext ULower.ext

end ULower

/-
Choice function for encodable types and decidable predicates.
We provide the following API

choose      {Œ± : Type*} {p : Œ± ‚Üí Prop} [c : encodable Œ±] [d : decidable_pred p] : (‚àÉ x, p x) ‚Üí Œ± :=
choose_spec {Œ± : Type*} {p : Œ± ‚Üí Prop} [c : encodable Œ±] [d : decidable_pred p] (ex : ‚àÉ x, p x) :
  p (choose ex) :=
-/
namespace Encodable

section FindA

variable {Œ± : Type*} (p : Œ± ‚Üí Prop) [Encodable Œ±] [DecidablePred p]

private def good : Option Œ± ‚Üí Prop
  | some a => p a
  | none => False

private def decidable_good : DecidablePred (good p) :=
  fun n => by
    cases n <;> unfold good <;> dsimp <;> infer_instance
    -- ‚ä¢ Decidable (Encodable.good p none)
                -- ‚ä¢ Decidable
                -- ‚ä¢ Decidable
                                -- ‚ä¢ Decidable False
                                -- ‚ä¢ Decidable (p val‚úù)
                                          -- üéâ no goals
                                          -- üéâ no goals
attribute [local instance] decidable_good

open Encodable

variable {p}

/-- Constructive choice function for a decidable subtype of an encodable type. -/
def chooseX (h : ‚àÉ x, p x) : { a : Œ± // p a } :=
  have : ‚àÉ n, good p (decode n) :=
    let ‚ü®w, pw‚ü© := h
    ‚ü®encode w, by simp [good, encodek, pw]‚ü©
                  -- üéâ no goals
  match (motive := ‚àÄ o, good p o ‚Üí { a // p a }) _, Nat.find_spec this with
  | some a, h => ‚ü®a, h‚ü©
#align encodable.choose_x Encodable.chooseX

/-- Constructive choice function for a decidable predicate over an encodable type. -/
def choose (h : ‚àÉ x, p x) : Œ± :=
  (chooseX h).1
#align encodable.choose Encodable.choose

theorem choose_spec (h : ‚àÉ x, p x) : p (choose h) :=
  (chooseX h).2
#align encodable.choose_spec Encodable.choose_spec

end FindA

/-- A constructive version of `Classical.axiom_of_choice` for `Encodable` types. -/
theorem axiom_of_choice {Œ± : Type*} {Œ≤ : Œ± ‚Üí Type*} {R : ‚àÄ x, Œ≤ x ‚Üí Prop} [‚àÄ a, Encodable (Œ≤ a)]
    [‚àÄ x y, Decidable (R x y)] (H : ‚àÄ x, ‚àÉ y, R x y) : ‚àÉ f : ‚àÄ a, Œ≤ a, ‚àÄ x, R x (f x) :=
  ‚ü®fun x => choose (H x), fun x => choose_spec (H x)‚ü©
#align encodable.axiom_of_choice Encodable.axiom_of_choice

/-- A constructive version of `Classical.skolem` for `Encodable` types. -/
theorem skolem {Œ± : Type*} {Œ≤ : Œ± ‚Üí Type*} {P : ‚àÄ x, Œ≤ x ‚Üí Prop} [‚àÄ a, Encodable (Œ≤ a)]
    [‚àÄ x y, Decidable (P x y)] : (‚àÄ x, ‚àÉ y, P x y) ‚Üî ‚àÉ f : ‚àÄ a, Œ≤ a, ‚àÄ x, P x (f x) :=
  ‚ü®axiom_of_choice, fun ‚ü®_, H‚ü© x => ‚ü®_, H x‚ü©‚ü©
#align encodable.skolem Encodable.skolem

/-
There is a total ordering on the elements of an encodable type, induced by the map to ‚Ñï.
-/
/-- The `encode` function, viewed as an embedding. -/
def encode' (Œ±) [Encodable Œ±] : Œ± ‚Ü™ ‚Ñï :=
  ‚ü®Encodable.encode, Encodable.encode_injective‚ü©
#align encodable.encode' Encodable.encode'

instance {Œ±} [Encodable Œ±] : IsTrans _ (encode' Œ± ‚Åª¬π'o (¬∑ ‚â§ ¬∑)) :=
  (RelEmbedding.preimage _ _).isTrans

instance {Œ±} [Encodable Œ±] : IsAntisymm _ (Encodable.encode' Œ± ‚Åª¬π'o (¬∑ ‚â§ ¬∑)) :=
  (RelEmbedding.preimage _ _).isAntisymm

instance {Œ±} [Encodable Œ±] : IsTotal _ (Encodable.encode' Œ± ‚Åª¬π'o (¬∑ ‚â§ ¬∑)) :=
  (RelEmbedding.preimage _ _).isTotal

end Encodable

namespace Directed

open Encodable

variable {Œ± : Type*} {Œ≤ : Type*} [Encodable Œ±] [Inhabited Œ±]

/-- Given a `Directed r` function `f : Œ± ‚Üí Œ≤` defined on an encodable inhabited type,
construct a noncomputable sequence such that `r (f (x n)) (f (x (n + 1)))`
and `r (f a) (f (x (encode a + 1))`. -/
protected noncomputable def sequence {r : Œ≤ ‚Üí Œ≤ ‚Üí Prop} (f : Œ± ‚Üí Œ≤) (hf : Directed r f) : ‚Ñï ‚Üí Œ±
  | 0 => default
  | n + 1 =>
    let p := Directed.sequence f hf n
    match (decode n: Option Œ±) with
    | none => Classical.choose (hf p p)
    | some a => Classical.choose (hf p a)
#align directed.sequence Directed.sequence

theorem sequence_mono_nat {r : Œ≤ ‚Üí Œ≤ ‚Üí Prop} {f : Œ± ‚Üí Œ≤} (hf : Directed r f) (n : ‚Ñï) :
    r (f (hf.sequence f n)) (f (hf.sequence f (n + 1))) := by
  dsimp [Directed.sequence]
  -- ‚ä¢ r (f (Directed.sequence f hf n))
  generalize hf.sequence f n = p
  -- ‚ä¢ r (f p)
  cases' h : (decode n: Option Œ±) with a
  ¬∑ exact (Classical.choose_spec (hf p p)).1
    -- üéâ no goals
  ¬∑ exact (Classical.choose_spec (hf p a)).1
    -- üéâ no goals
#align directed.sequence_mono_nat Directed.sequence_mono_nat

theorem rel_sequence {r : Œ≤ ‚Üí Œ≤ ‚Üí Prop} {f : Œ± ‚Üí Œ≤} (hf : Directed r f) (a : Œ±) :
    r (f a) (f (hf.sequence f (encode a + 1))) := by
  simp only [Directed.sequence, add_eq, add_zero, encodek, and_self]
  -- ‚ä¢ r (f a) (f (Classical.choose (_ : ‚àÉ x, (fun x => r (f (Directed.sequence f h ‚Ä¶
  exact (Classical.choose_spec (hf _ a)).2
  -- üéâ no goals
#align directed.rel_sequence Directed.rel_sequence

variable [Preorder Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Directed (¬∑ ‚â§ ¬∑) f)

theorem sequence_mono : Monotone (f ‚àò hf.sequence f) :=
  monotone_nat_of_le_succ <| hf.sequence_mono_nat
#align directed.sequence_mono Directed.sequence_mono

theorem le_sequence (a : Œ±) : f a ‚â§ f (hf.sequence f (encode a + 1)) :=
  hf.rel_sequence a
#align directed.le_sequence Directed.le_sequence

end Directed

section Quotient

open Encodable Quotient

variable {Œ± : Type*} {s : Setoid Œ±} [@DecidableRel Œ± (¬∑ ‚âà ¬∑)] [Encodable Œ±]

/-- Representative of an equivalence class. This is a computable version of `Quot.out` for a setoid
on an encodable type. -/
def Quotient.rep (q : Quotient s) : Œ± :=
  choose (exists_rep q)
#align quotient.rep Quotient.rep

theorem Quotient.rep_spec (q : Quotient s) : ‚ü¶q.rep‚üß = q :=
  choose_spec (exists_rep q)
#align quotient.rep_spec Quotient.rep_spec

/-- The quotient of an encodable space by a decidable equivalence relation is encodable. -/
def encodableQuotient : Encodable (Quotient s) :=
  ‚ü®fun q => encode q.rep, fun n => Quotient.mk'' <$> decode n, by
    rintro ‚ü®l‚ü©; dsimp; rw [encodek]; exact congr_arg some ‚ü¶l‚üß.rep_spec‚ü©
    -- ‚ä¢ (fun n => Quotient.mk'' <$> decode n) ((fun q => encode (rep q)) (Quot.mk Se ‚Ä¶
                -- ‚ä¢ Option.map Quotient.mk'' (decode (encode (rep (Quot.mk Setoid.r l)))) = some ‚Ä¶
                       -- ‚ä¢ Option.map Quotient.mk'' (some (rep (Quot.mk Setoid.r l))) = some (Quot.mk S ‚Ä¶
                                     -- üéâ no goals
#align encodable_quotient encodableQuotient

end Quotient
