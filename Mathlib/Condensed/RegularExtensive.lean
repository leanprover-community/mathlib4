/-
Copyright (c) 2023 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson, Filippo A. E. Nuccio, Riccardo Brasca
-/
import Mathlib.CategoryTheory.Limits.Preserves.Finite
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Products
import Mathlib.CategoryTheory.Preadditive.Projective
import Mathlib.CategoryTheory.Sites.RegularExtensive
/-!
# The Regular and Extensive Coverages
This file defines two coverages on a category `C`.
The first one is called the *regular* coverage and for that to exist, the category `C` must satisfy
a condition called `Preregular C`. This means that effective epimorphisms can be "pulled back". The
covering sieves of this coverage are generated by presieves consisting of a single effective
epimorphism.
The second one is called the *extensive* coverage and for that to exist, the category `C` must
satisfy a condition called `Extensive C`. This means `C` has finite coproducts and that those
are preserved by pullbacks. The covering sieves of this coverage are generated by presieves
consisting finitely many arrows that together induce an isomorphism from the coproduct to the
target.
In `extensive_union_regular_generates_coherent`, we prove that the union of these two coverages
generates the coherent topology on `C` if `C` is precoherent, extensive and regular.
TODO: figure out under what conditions `Preregular` and `Extensive` are implied by `Precoherent` and
vice versa.
-/

universe v u w

namespace CategoryTheory

open Limits

variable (C : Type u) [Category.{v} C]

variable {C}

/-- The union of the extensive and regular coverages generates the coherent topology on `C`. -/
lemma extensive_regular_generate_coherent [Preregular C] [Extensive C] [Precoherent C] :
    ((extensiveCoverage C) âŠ” (regularCoverage C)).toGrothendieck =
    (coherentTopology C) := by
  ext B S
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· induction h with
    | of Y T hT =>
      apply Coverage.saturate.of
      simp only [Coverage.sup_covering, Set.mem_union] at hT
      exact Or.elim hT
        (fun âŸ¨Î±, x, X, Ï€, âŸ¨h, _âŸ©âŸ© â†¦ âŸ¨Î±, x, X, Ï€, âŸ¨h, inferInstanceâŸ©âŸ©)
        (fun âŸ¨Z, f, âŸ¨h, _âŸ©âŸ© â†¦ âŸ¨Unit, inferInstance, fun _ â†¦ Z, fun _ â†¦ f, âŸ¨h, inferInstanceâŸ©âŸ©)
    | top => apply Coverage.saturate.top
    | transitive Y T => apply Coverage.saturate.transitive Y T<;> [assumption; assumption]
  Â· induction h with
    | of Y T hT =>
      obtain âŸ¨I, hI, X, f, âŸ¨h, hTâŸ©âŸ© := hT
      let Ï† := fun (i : I) â†¦ Sigma.Î¹ X i
      let F := Sigma.desc f
      let Z := Sieve.generate T
      let Xs := (âˆ fun (i : I) => X i)
      let Zf := Sieve.generate (Presieve.ofArrows (fun (_ : Unit) â†¦ Xs) (fun (_ : Unit) â†¦ F))
      apply Coverage.saturate.transitive Y Zf
      Â· apply Coverage.saturate.of
        simp only [Coverage.sup_covering, extensiveCoverage, regularCoverage, Set.mem_union,
          Set.mem_setOf_eq]
        exact Or.inr âŸ¨Xs, F, âŸ¨rfl, inferInstanceâŸ©âŸ©
      Â· intro R g hZfg
        dsimp at hZfg
        rw [Presieve.ofArrows_pUnit] at hZfg
        obtain âŸ¨W, Ïˆ, Ïƒ, âŸ¨hW, hW'âŸ©âŸ© := hZfg
        induction hW
        rw [â† hW', Sieve.pullback_comp Z]
        suffices Sieve.pullback Ïˆ ((Sieve.pullback F) Z) âˆˆ GrothendieckTopology.sieves
          ((extensiveCoverage C) âŠ” (regularCoverage C)).toGrothendieck R by assumption
        apply GrothendieckTopology.pullback_stable'
        suffices Coverage.saturate ((extensiveCoverage C) âŠ” (regularCoverage C)) Xs
          (Z.pullback F) by assumption
        suffices : Sieve.generate (Presieve.ofArrows X Ï†) â‰¤ Z.pullback F
        Â· apply Coverage.saturate_of_superset _ this
          apply Coverage.saturate.of
          simp only [Coverage.sup_covering, extensiveCoverage, regularCoverage, Set.mem_union,
            Set.mem_setOf_eq]
          refine Or.inl âŸ¨I, hI, X, Ï†, âŸ¨rfl, ?_âŸ©âŸ©
          suffices Sigma.desc Ï† = ğŸ™ _ by rw [this]; infer_instance
          ext
          simp only [colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app, Category.comp_id]
        intro Q q hq
        simp only [Sieve.pullback_apply, Sieve.generate_apply]
        simp only [Sieve.generate_apply] at hq
        obtain âŸ¨E, e, r, hqâŸ© := hq
        refine' âŸ¨E, e, r â‰« F, âŸ¨_, _âŸ©âŸ©
        Â· rw [h]
          induction hq.1
          simp only [colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app]
          exact Presieve.ofArrows.mk _
        Â· rw [â† hq.2]
          simp only [Category.assoc]
    | top => apply Coverage.saturate.top
    | transitive Y T => apply Coverage.saturate.transitive Y T<;> [assumption; assumption]

section ExtensiveSheaves

variable [Extensive C]

/-- A presieve is *extensive* if it is finite and its arrows induce an isomorphism from the
coproduct to the target. -/
class _root_.CategoryTheory.Presieve.extensive [HasFiniteCoproducts C] {X : C} (R : Presieve X) :
    Prop where
  /-- `R` consists of a finite collection of arrows that together induce an isomorphism from the
  coproduct of their sources. -/
  arrows_sigma_desc_iso : âˆƒ (Î± : Type) (_ : Fintype Î±) (Z : Î± â†’ C) (Ï€ : (a : Î±) â†’ (Z a âŸ¶ X)),
    R = Presieve.ofArrows Z Ï€ âˆ§ IsIso (Sigma.desc Ï€)

instance {X : C} (S : Presieve X) [S.extensive] : S.hasPullbacks where
  has_pullbacks := by
    obtain âŸ¨_, _, _, _, hS, _âŸ© := Presieve.extensive.arrows_sigma_desc_iso (R := S)
    intro _ _ f hf _ hg
    rw [hS] at hf hg
    cases' hg with b
    apply HasPullbacksOfInclusions.has_pullback f

namespace ExtensiveSheafConditionProof

lemma sigma_surjective {Î± : Type} {Z : Î± â†’ C} {X : C} (Ï€ : (a : Î±) â†’ Z a âŸ¶ X) :
    Function.Surjective (fun a => âŸ¨Z a, Ï€ a, Presieve.ofArrows.mk aâŸ© :
    Î± â†’ Î£(Y : C), { f : Y âŸ¶ X // Presieve.ofArrows Z Ï€ f }) :=
  fun âŸ¨_, âŸ¨_, hfâŸ©âŸ© â†¦ by cases' hf with a _; exact âŸ¨a, rflâŸ©

open Opposite

instance {Î± : Type} {Z : Î± â†’ C} {X : C} {Ï€ : (a : Î±) â†’ Z a âŸ¶ X} [Fintype Î±] :
    HasProduct fun (x : Î£(Y : C), { f : Y âŸ¶ X // Presieve.ofArrows Z Ï€ f }) â†¦ (op x.1) :=
  haveI := Finite.of_surjective _ (sigma_surjective Ï€)
  inferInstance

/-- The canonical map from `Equalizer.FirstObj` to a product indexed by `Î±` -/
noncomputable
def prod_map {Î± : Type} {Z : Î± â†’ C} {X : C} (Ï€ : (a : Î±) â†’ Z a âŸ¶ X) (F : Cáµ’áµ– â¥¤ Type max u v) :
    (âˆ fun (f : (Î£(Y : C), { f : Y âŸ¶ X // Presieve.ofArrows Z Ï€ f })) => F.obj (op f.fst)) âŸ¶
    âˆ fun a => F.obj (op (Z a)) :=
  Pi.lift (fun a => Pi.Ï€ _ âŸ¨Z a, Ï€ a, Presieve.ofArrows.mk aâŸ©) â‰« ğŸ™ _

/-- The inverse to `Equalizer.forkMap F (Presieve.ofArrows Z Ï€)`. -/
noncomputable
def firstObj_to_base {Î± : Type} [Fintype Î±] {Z : Î± â†’ C} {X : C} (Ï€ : (a : Î±) â†’ Z a âŸ¶ X)
  (F : Cáµ’áµ– â¥¤ Type max u v) [PreservesFiniteProducts F] [IsIso (Sigma.desc Ï€)] :
    Equalizer.FirstObj F (Presieve.ofArrows Z Ï€) âŸ¶ F.obj (op X) :=
  haveI : PreservesLimit (Discrete.functor fun a => op (Z a)) F :=
    (PreservesFiniteProducts.preserves Î±).preservesLimit
  (prod_map Ï€ F) â‰« ((Limits.PreservesProduct.iso F (fun a => op <| Z a)).inv â‰«
    F.map (opCoproductIsoProduct Z).inv â‰« F.map (inv (Sigma.desc Ï€).op))

lemma comp_inv_desc_eq_Î¹ {Î± : Type} [Fintype Î±] {Z : Î± â†’ C} {X : C} (Ï€ : (a : Î±) â†’ Z a âŸ¶ X)
    [IsIso (Sigma.desc Ï€)] (a : Î±) : Ï€ a â‰« inv (Sigma.desc Ï€) = Sigma.Î¹ _ a := by
  simp only [IsIso.comp_inv_eq, colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app]

@[simp]
lemma PreservesProduct.isoInvCompMap {C : Type u} [Category C] {D : Type v} [Category D] (F : C â¥¤ D)
    {J : Type w} {f : J â†’ C} [HasProduct f] [HasProduct (fun j => F.obj (f j))]
    [PreservesLimit (Discrete.functor f) F] (j : J) :
    (PreservesProduct.iso F f).inv â‰« F.map (Pi.Ï€ _ j) = Pi.Ï€ _ j :=
  IsLimit.conePointUniqueUpToIso_inv_comp _ (limit.isLimit _) (âŸ¨jâŸ© : Discrete J)

instance {Î± : Type} [Fintype Î±] {Z : Î± â†’ C} {F : C â¥¤ Type w}
    [PreservesFiniteProducts F] : PreservesLimit (Discrete.functor fun a => (Z a)) F :=
  (PreservesFiniteProducts.preserves Î±).preservesLimit

instance {X : C} (S : Presieve X) [S.extensive]
    {F : Cáµ’áµ– â¥¤ Type max u v} [PreservesFiniteProducts F] : IsIso (Equalizer.forkMap F S) := by
  obtain âŸ¨Î±, _, Z, Ï€, hS, _âŸ© := Presieve.extensive.arrows_sigma_desc_iso (R := S)
  subst hS
  refine' âŸ¨firstObj_to_base Ï€ F,_,_âŸ©
  Â· simp only [firstObj_to_base, â† Category.assoc, Functor.map_inv,
      IsIso.comp_inv_eq, Category.id_comp, â† Functor.mapIso_inv, Iso.comp_inv_eq,
      Functor.mapIso_hom, Iso.comp_inv_eq, â† Functor.map_comp,
      desc_op_comp_opCoproductIsoProduct_hom, PreservesProduct.iso_hom, map_lift_piComparison,
      colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app]
    funext s
    ext a
    simp only [prod_map, types_comp_apply, types_id_apply, Types.Limit.lift_Ï€_apply,
      Fan.mk_pt, Equalizer.forkMap, Fan.mk_Ï€_app, Types.pi_lift_Ï€_apply]
  Â· refine Limits.Pi.hom_ext _ _ (fun f => ?_)
    simp only [Equalizer.forkMap, Category.assoc, limit.lift_Ï€, Fan.mk_pt, Fan.mk_Ï€_app,
      Category.id_comp]
    obtain âŸ¨a, haâŸ© := sigma_surjective Ï€ f
    rw [firstObj_to_base, Category.assoc, Category.assoc, Category.assoc, â† Functor.map_comp,
      â† op_inv, â† op_comp, â† ha, comp_inv_desc_eq_Î¹, â† Functor.map_comp,
      opCoproductIsoProduct_inv_comp_Î¹, PreservesProduct.isoInvCompMap F a]
    simp only [prod_map, Category.comp_id, limit.lift_Ï€, Fan.mk_pt, Fan.mk_Ï€_app]

end ExtensiveSheafConditionProof

open ExtensiveSheafConditionProof in
lemma isSheafFor_extensive_of_preservesFiniteProducts {X : C} (S : Presieve X) [S.extensive]
    (F : Cáµ’áµ– â¥¤ Type max u v) [PreservesFiniteProducts F] :
    Presieve.IsSheafFor F S := by
  refine' (Equalizer.Presieve.sheaf_condition F S).2 _
  rw [Limits.Types.type_equalizer_iff_unique]
  dsimp [Equalizer.FirstObj]
  suffices : IsIso (Equalizer.forkMap F S)
  Â· intro y _
    refine' âŸ¨inv (Equalizer.forkMap F S) y, _, fun yâ‚ hyâ‚ => _âŸ©
    Â· change (inv (Equalizer.forkMap F S) â‰« (Equalizer.forkMap F S)) y = y
      rw [IsIso.inv_hom_id, types_id_apply]
    Â· replace hyâ‚ := congr_arg (inv (Equalizer.forkMap F S)) hyâ‚
      change ((Equalizer.forkMap F S) â‰« inv (Equalizer.forkMap F S)) _ = _ at hyâ‚
      rwa [IsIso.hom_inv_id, types_id_apply] at hyâ‚
  infer_instance

end ExtensiveSheaves

section RegularSheaves

open Opposite

/-- A presieve is *regular* if it consists of a single effective epimorphism. -/
class _root_.CategoryTheory.Presieve.regular {X : C} (R : Presieve X) : Prop where
  /-- `R` consists of a single epimorphism. -/
  single_epi : âˆƒ (Y : C) (f : Y âŸ¶ X), R = Presieve.ofArrows (fun (_ : Unit) â†¦ Y)
    (fun (_ : Unit) â†¦ f) âˆ§ EffectiveEpi f

def MapToEqualizer (P : Cáµ’áµ– â¥¤ Type (max u v)) {W X B : C} (f : X âŸ¶ B)
    (gâ‚ gâ‚‚ : W âŸ¶ X) (w : gâ‚ â‰« f = gâ‚‚ â‰« f) :
    P.obj (op B) â†’ { x : P.obj (op X) | P.map gâ‚.op x = P.map gâ‚‚.op x } :=
  fun t â†¦ âŸ¨P.map f.op t, by
    change (P.map _ â‰« P.map _) _ = (P.map _ â‰« P.map _) _ ;
    simp_rw [â† P.map_comp, â† op_comp, w] âŸ©

def EqualizerCondition (P : Cáµ’áµ– â¥¤ Type (max u v)) : Prop :=
  âˆ€ (X B : C) (Ï€ : X âŸ¶ B) [EffectiveEpi Ï€] [HasPullback Ï€ Ï€], Function.Bijective
    (MapToEqualizer P Ï€ (pullback.fst (f := Ï€) (g := Ï€)) (pullback.snd (f := Ï€) (g := Ï€))
    pullback.condition)

noncomputable
def EqualizerFirstObjIso (F : Cáµ’áµ– â¥¤ Type (max u v)) {B X : C} (Ï€ : X âŸ¶ B)
     : Equalizer.FirstObj F (Presieve.singleton Ï€) â‰… F.obj (op X) :=
  CategoryTheory.Equalizer.firstObjEqFamily F (Presieve.singleton Ï€) â‰ªâ‰«
  { hom := fun e â†¦ e Ï€ (Presieve.singleton_self Ï€)
    inv := fun e _ _ h â†¦ by
      induction h with
      | mk => exact e
    hom_inv_id := by
      funext _ _ _ h
      induction h with
      | mk => rfl
    inv_hom_id := by aesop }

instance {B X : C} (Ï€ : X âŸ¶ B) [EffectiveEpi Ï€] [HasPullback Ï€ Ï€] :
    (Presieve.singleton Ï€).hasPullbacks where
  has_pullbacks hf _ hg := by
    cases hf
    cases hg
    infer_instance

noncomputable
def EqualizerSecondObjIso (F : Cáµ’áµ– â¥¤ Type (max u v)) {B X : C} (Ï€ : X âŸ¶ B) [EffectiveEpi Ï€]
    [HasPullback Ï€ Ï€] :
    Equalizer.Presieve.SecondObj F (Presieve.singleton Ï€) â‰… F.obj (op (Limits.pullback Ï€ Ï€)) :=
  Types.productIso.{max u v, max u v} _ â‰ªâ‰«
  { hom := fun e â†¦ e (âŸ¨X, âŸ¨Ï€, Presieve.singleton_self Ï€âŸ©âŸ©, âŸ¨X, âŸ¨Ï€, Presieve.singleton_self Ï€âŸ©âŸ©)
    inv := fun x âŸ¨âŸ¨_, âŸ¨_, hâ‚âŸ©âŸ© , âŸ¨_, âŸ¨_, hâ‚‚âŸ©âŸ©âŸ© â†¦ by
      induction hâ‚
      induction hâ‚‚
      exact x
    hom_inv_id := by
      funext _ âŸ¨âŸ¨_, âŸ¨_, hâ‚âŸ©âŸ© , âŸ¨_, âŸ¨_, hâ‚‚âŸ©âŸ©âŸ©
      induction hâ‚
      induction hâ‚‚
      rfl
    inv_hom_id := by aesop }

lemma isSheafFor_regular_of_hasPullbacks {B : C} {S : Presieve B} [S.regular] [S.hasPullbacks]
     {F : Cáµ’áµ– â¥¤ Type (max u v)}
    (hFecs : EqualizerCondition F) : S.IsSheafFor F := by
  obtain âŸ¨X, Ï€, âŸ¨hS, Ï€surjâŸ©âŸ© := Presieve.regular.single_epi (R := S)
  rw [Presieve.ofArrows_pUnit] at hS
  haveI hh : (Presieve.singleton Ï€).hasPullbacks := by rw [â† hS]; infer_instance
  haveI : HasPullback Ï€ Ï€ := hh.has_pullbacks (Presieve.singleton.mk) (Presieve.singleton.mk)
  subst hS
  rw [Equalizer.Presieve.sheaf_condition, Limits.Types.type_equalizer_iff_unique]
  intro y h
  specialize hFecs X B Ï€
  have fork_comp : Equalizer.forkMap F (Presieve.singleton Ï€) â‰« (EqualizerFirstObjIso F Ï€).hom =
      F.map Ï€.op
  Â· dsimp [EqualizerFirstObjIso, Equalizer.forkMap]
    ext b
    simp only [types_comp_apply, Equalizer.firstObjEqFamily_hom, Types.pi_lift_Ï€_apply]
  have fmap_comp : (EqualizerFirstObjIso F Ï€).hom â‰« F.map (pullback.fst (f := Ï€) (g := Ï€)).op =
      Equalizer.Presieve.firstMap F (Presieve.singleton Ï€) â‰« (EqualizerSecondObjIso F Ï€).hom
  Â· dsimp [EqualizerSecondObjIso, EqualizerFirstObjIso, Equalizer.Presieve.firstMap]
    ext b
    simp only [types_comp_apply, Equalizer.firstObjEqFamily_hom, Types.pi_lift_Ï€_apply]
  have smap_comp : (EqualizerFirstObjIso F Ï€).hom â‰« F.map (pullback.snd (f := Ï€) (g := Ï€)).op =
      Equalizer.Presieve.secondMap F (Presieve.singleton Ï€) â‰« (EqualizerSecondObjIso F Ï€).hom
  Â· dsimp [EqualizerSecondObjIso, EqualizerFirstObjIso, Equalizer.Presieve.secondMap]
    ext b
    simp only [types_comp_apply, Equalizer.firstObjEqFamily_hom, Types.pi_lift_Ï€_apply]
  have iy_mem : F.map (pullback.fst (f := Ï€) (g := Ï€)).op ((EqualizerFirstObjIso F Ï€).hom y) =
      F.map (pullback.snd (f := Ï€) (g := Ï€)).op ((EqualizerFirstObjIso F Ï€).hom y)
  Â· change ((EqualizerFirstObjIso F Ï€).hom â‰« _) y = _
    apply Eq.symm -- how do I avoid this ugly hack?
    change ((EqualizerFirstObjIso F Ï€).hom â‰« _) y = _
    rw [fmap_comp, smap_comp]
    dsimp
    rw [h]
  have uniq_F : âˆƒ! x, F.map Ï€.op x = (EqualizerFirstObjIso F Ï€).hom y
  Â· rw [Function.bijective_iff_existsUnique] at hFecs
    specialize hFecs âŸ¨(EqualizerFirstObjIso F Ï€).hom y, iy_memâŸ©
    obtain âŸ¨x, hxâŸ© := hFecs
    refine' âŸ¨x, _âŸ©
    dsimp [MapToEqualizer] at *
    refine' âŸ¨Subtype.ext_iff.mp hx.1,_âŸ©
    intro z hz
    apply hx.2
    rwa [Subtype.ext_iff]
  obtain âŸ¨x,hxâŸ© := uniq_F
  dsimp at hx
  rw [â† fork_comp] at hx
  use x
  dsimp
  constructor
  Â· apply_fun (EqualizerFirstObjIso F Ï€).hom
    Â· exact hx.1
    Â· apply Function.Bijective.injective
      rw [â† isIso_iff_bijective]
      exact inferInstance
  Â· intro z hz
    apply_fun (EqualizerFirstObjIso F Ï€).hom at hz
    exact hx.2 z hz

lemma isSheafFor_regular_of_projective {X : C} (S : Presieve X) [S.regular] [Projective X]
    (F : Cáµ’áµ– â¥¤ Type (max u v)) : S.IsSheafFor F := by
  obtain âŸ¨Y, f, rfl, hfâŸ© := Presieve.regular.single_epi (R := S)
  let g := Projective.factorThru (ğŸ™ _) f
  have hfg : g â‰« f = ğŸ™ _ := by
    simp only [Projective.factorThru_comp]
  intro y hy
  refine' âŸ¨F.map g.op <| y f <| Presieve.ofArrows.mk (), fun Z h hZ => _, fun z hz => _âŸ©
  Â· cases' hZ with u
    have := hy (fâ‚ := f) (fâ‚‚ := f) (ğŸ™ Y) (f â‰« g) (Presieve.ofArrows.mk ())
        (Presieve.ofArrows.mk ()) ?_
    Â· rw [op_id, F.map_id, types_id_apply] at this
      rw [â† types_comp_apply (F.map g.op) (F.map f.op), â† F.map_comp, â† op_comp]
      exact this.symm
    Â· rw [Category.id_comp, Category.assoc, hfg, Category.comp_id]
  Â· have := congr_arg (F.map g.op) <| hz f (Presieve.ofArrows.mk ())
    rwa [â† types_comp_apply (F.map f.op) (F.map g.op), â† F.map_comp, â† op_comp, hfg, op_id,
      F.map_id, types_id_apply] at this

end RegularSheaves

end CategoryTheory
