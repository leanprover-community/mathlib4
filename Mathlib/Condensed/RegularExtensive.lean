/-
Copyright (c) 2023 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson, Filippo A. E. Nuccio, Riccardo Brasca
-/
import Mathlib.CategoryTheory.Limits.Preserves.Finite
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Products
import Mathlib.CategoryTheory.Sites.Coherent
/-!
# The Regular and Extensive Coverages
This file defines two coverages on a category `C`.
The first one is called the *regular* coverage and for that to exist, the category `C` must satisfy
a condition called `Regular C`. This means that effective epimorphisms can be "pulled back". The
covering sieves of this coverage are generated by presieves consisting of a single effective
epimorphism.
The second one is called the *extensive* coverage and for that to exist, the category `C` must
satisfy a condition called `Extensive C`. This means `C` has finite coproducts and that those
are preserved by pullbacks. The covering sieves of this coverage are generated by presieves
consisting finitely many arrows that together induce an isomorphism from the coproduct to the
target.
## Main results
* In `extensive_union_regular_generates_coherent`, we prove that the union of these two coverages
  generates the coherent topology on `C` if `C` is precoherent, extensive and regular.
* In `isSheafFor_extensive_of_preservesFiniteProducts` we prove that finite-product preserving
  presheaves are sheaves for extensive sieves.
TODO: figure out under what conditions `Regular` and `Extensive` are implied by `Precoherent`.
-/

universe v u w

namespace CategoryTheory

namespace Coverage

variable {C : Type u} [Category.{v} C]

/-- The union of two coverages is a coverage. -/
@[simps]
def union (x y : Coverage C) : Coverage C where
  covering B := x.covering B âˆª y.covering B
  pullback := by
    rintro X Y f S (hx | hy)
    Â· obtain âŸ¨T, hTâŸ© := x.pullback f S hx
      exact âŸ¨T, Or.inl hT.1, hT.2âŸ©
    Â· obtain âŸ¨T, hTâŸ© := y.pullback f S hy
      exact âŸ¨T, Or.inr hT.1, hT.2âŸ©

end Coverage

open Limits

namespace Presieve

variable {C : Type u} [Category.{v} C] {X : C}

/-- A presieve is *extensive* if it is finite and its arrows induce an isomorphism from the
coproduct to the target. -/
class extensive [HasFiniteCoproducts C] (R : Presieve X) : Prop where
  /-- `R` consists of a finite collection of arrows that together induce an isomorphism from the
  coproduct of their sources. -/
  arrows_sigma_desc_iso : âˆƒ (Î± : Type) (_ : Fintype Î±) (Z : Î± â†’ C) (Ï€ : (a : Î±) â†’ (Z a âŸ¶ X)),
    R = Presieve.ofArrows Z Ï€ âˆ§ IsIso (Sigma.desc Ï€)

/-- A presieve is *regular* if it consists of a single effective epimorphism. -/
class regular (R : Presieve X) : Prop where
  /-- `R` consists of a single epimorphism. -/
  single_epi : âˆƒ (Y : C) (f : Y âŸ¶ X), R = Presieve.ofArrows (fun (_ : Unit) â†¦ Y)
    (fun (_ : Unit) â†¦ f) âˆ§ EffectiveEpi f

end Presieve

section instances

variable {C : Type u} [Category.{v} C]

/--
Given an `EffectiveEpiFamily X Ï€` such that the coproduct of `X` exists, `Sigma.desc Ï€` is an
`EffectiveEpi`.
-/
noncomputable
def EffectiveEpiFamily_descStruct {B : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
    [HasCoproduct X] [EffectiveEpiFamily X Ï€] : EffectiveEpiStruct (Sigma.desc Ï€) where
  desc e h := EffectiveEpiFamily.desc X Ï€ (fun a â†¦ Sigma.Î¹ X a â‰« e) (fun aâ‚ aâ‚‚ gâ‚ gâ‚‚ hg â†¦ by
    simp only [â† Category.assoc]
    apply h (gâ‚ â‰« Sigma.Î¹ X aâ‚) (gâ‚‚ â‰« Sigma.Î¹ X aâ‚‚)
    simpa only [Category.assoc, colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app])
  fac e h := by
    ext a
    simp only [colimit.Î¹_desc_assoc, Discrete.functor_obj, Cofan.mk_pt, Cofan.mk_Î¹_app,
      EffectiveEpiFamily.fac]
  uniq e _ m hm := by
    have := EffectiveEpiFamily.uniq X Ï€ (fun a â†¦ Sigma.Î¹ X a â‰« e) ?_ m
    Â· apply this
      intro a
      rw [â† hm]
      simp only [colimit.Î¹_desc_assoc, Discrete.functor_obj, Cofan.mk_pt, Cofan.mk_Î¹_app]
    Â· intro Z aâ‚ aâ‚‚ gâ‚ gâ‚‚ hg
      rw [â† hm]
      simp only [colimit.Î¹_desc_assoc, Discrete.functor_obj, Cofan.mk_pt, Cofan.mk_Î¹_app]
      rw [â† Category.assoc, hg, Category.assoc]

instance {B : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) [HasCoproduct X]
    [EffectiveEpiFamily X Ï€] : EffectiveEpi (Sigma.desc Ï€) :=
  âŸ¨âŸ¨EffectiveEpiFamily_descStruct X Ï€âŸ©âŸ©

/--
An `EffectiveEpiFamily` consisting of a single `EffectiveEpi`
-/
noncomputable
def EffectiveEpi_familyStruct {B X : C} (f : X âŸ¶ B) [EffectiveEpi f] :
    EffectiveEpiFamilyStruct (fun () â†¦ X) (fun () â†¦ f) where
  desc e h := EffectiveEpi.desc f (e ()) (fun gâ‚ gâ‚‚ hg â†¦ h () () gâ‚ gâ‚‚ hg)
  fac e h := fun _ â†¦ EffectiveEpi.fac f (e ()) (fun gâ‚ gâ‚‚ hg â†¦ h () () gâ‚ gâ‚‚ hg)
  uniq e h m hm := by apply EffectiveEpi.uniq f (e ()) (h () ()); exact hm ()

instance {B X : C} (f : X âŸ¶ B) [EffectiveEpi f] : EffectiveEpiFamily (fun () â†¦ X) (fun () â†¦ f) :=
  âŸ¨âŸ¨EffectiveEpi_familyStruct fâŸ©âŸ©

/--
A family of morphisms with the same target inducing an isomorphism from the coproduct to the target
is an `EffectiveEpiFamily`.
-/
noncomputable
def EffectiveEpiFamilyStruct_of_isIso_desc {B : C} {Î± : Type*} (X : Î± â†’ C)
    (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) [HasCoproduct X] [IsIso (Sigma.desc Ï€)] :
    EffectiveEpiFamilyStruct X Ï€ where
  desc e _ := (asIso (Sigma.desc Ï€)).inv â‰« (Sigma.desc e)
  fac e h := by
    intro a
    have : Ï€ a = Sigma.Î¹ X a â‰« (asIso (Sigma.desc Ï€)).hom := by simp only [asIso_hom,
      colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app]
    rw [this, Category.assoc]
    simp only [asIso_hom, asIso_inv, IsIso.hom_inv_id_assoc, colimit.Î¹_desc, Cofan.mk_pt,
      Cofan.mk_Î¹_app]
  uniq e h m hm := by
    simp only [asIso_inv, IsIso.eq_inv_comp]
    ext a
    simp only [colimit.Î¹_desc_assoc, Discrete.functor_obj, Cofan.mk_pt, Cofan.mk_Î¹_app,
      colimit.Î¹_desc]
    exact hm a

instance {B : C} {Î± : Type*} (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) [HasCoproduct X]
    [IsIso (Sigma.desc Ï€)] : EffectiveEpiFamily X Ï€ :=
  âŸ¨âŸ¨EffectiveEpiFamilyStruct_of_isIso_desc X Ï€âŸ©âŸ©

end instances

variable (C : Type u) [Category.{v} C]

/--
The condition `Regular C` is property that effective epis can be "pulled back" along any
morphism. This is satisfied e.g. by categories that have pullbacks that preserve effective
epimorphisms (like `Profinite` and `CompHaus`), and categories where every object is projective
(like  `Stonean`).
-/
class Regular : Prop where
  /--
  For `X`, `Y`, `Z`, `f`, `g` like in the diagram, where `g` is an effective epi, there exists
  an object `W`, a morphism `i : W âŸ¶ Z` and an effective epi `h : W âŸ¶ X` making the diagram
  commute.
  ```
  W --i-â†’ Z
  |       |
  h       g
  â†“       â†“
  X --f-â†’ Y
  ```
  -/
  exists_fac : âˆ€ {X Y Z : C} (f : X âŸ¶ Y) (g : Z âŸ¶ Y) [EffectiveEpi g],
    (âˆƒ (W : C) (h : W âŸ¶ X) (_ : EffectiveEpi h) (i : W âŸ¶ Z), i â‰« g = h â‰« f)

/--
Describes the property of having pullbacks of morphsims into a finite coproduct, where one
of the morphisms is an inclusion map into the coproduct (up to isomorphism).
-/
class HasPullbacksOfInclusions : Prop where
    /-- For any morphism `f : X âŸ¶ Z`, where `Z` is the coproduct of `i : (a : Î±) â†’ Y a âŸ¶ Z` with
    `Î±` finite, the pullback of `f` and `i a` exists for every `a : Î±`. -/
    has_pullback : âˆ€ {X Z : C} {Î± : Type _} (f : X âŸ¶ Z) {Y : (a : Î±) â†’ C}
    (i : (a : Î±) â†’ Y a âŸ¶ Z) [Fintype Î±] [HasCoproduct Y] [IsIso (Sigma.desc i)] (a : Î±),
    HasPullback f (i a)

instance [HasPullbacksOfInclusions C] {X Z : C} {Î± : Type _} (f : X âŸ¶ Z) {Y : (a : Î±) â†’ C}
    (i : (a : Î±) â†’ Y a âŸ¶ Z) [Fintype Î±] [HasCoproduct Y] [IsIso (Sigma.desc i)] (a : Î±) :
    HasPullback f (i a) := HasPullbacksOfInclusions.has_pullback f i a

/--
If `C` has pullbacks then it has the pullbacks relevant to `HasPullbacksOfInclusions`.
-/
instance (priority := 10) [HasPullbacks C] :
  HasPullbacksOfInclusions C := âŸ¨fun _ _ _ => inferInstanceâŸ©

/--
A category is *extensive* if it has all finite coproducts and those coproducts are preserved
by pullbacks (we only require the relevant pullbacks to exist, via `HasPullbacksOfInclusions`).
-/
class Extensive extends HasFiniteCoproducts C, HasPullbacksOfInclusions C : Prop where
  /-- Pulling back an isomorphism from a coproduct yields an isomorphism. -/
  sigma_desc_iso : âˆ€ {Î± : Type} [Fintype Î±] {X : C} {Z : Î± â†’ C} (Ï€ : (a : Î±) â†’ Z a âŸ¶ X)
    {Y : C} (f : Y âŸ¶ X) (_ : IsIso (Sigma.desc Ï€)),
    IsIso (Sigma.desc ((fun _ â†¦ pullback.fst) : (a : Î±) â†’ pullback f (Ï€ a) âŸ¶ _))

/--
The regular coverage on a regular category `C`.
-/
def regularCoverage [Regular C] : Coverage C where
  covering B := { S | âˆƒ (X : C) (f : X âŸ¶ B), S = Presieve.ofArrows (fun (_ : Unit) â†¦ X)
    (fun (_ : Unit) â†¦ f) âˆ§ EffectiveEpi f }
  pullback := by
    intro X Y f S âŸ¨Z, Ï€, hÏ€, h_epiâŸ©
    have := Regular.exists_fac f Ï€
    obtain âŸ¨W, h, _, i, thisâŸ© := this
    refine âŸ¨Presieve.singleton h, âŸ¨?_, ?_âŸ©âŸ©
    Â· exact âŸ¨W, h, by {rw [Presieve.ofArrows_pUnit h]}, inferInstanceâŸ©
    Â· intro W g hg
      cases hg
      refine âŸ¨Z, i, Ï€, âŸ¨?_, thisâŸ©âŸ©
      cases hÏ€
      rw [Presieve.ofArrows_pUnit]
      exact Presieve.singleton.mk

/--
The extensive coverage on an extensive category `C`
-/
def extensiveCoverage [Extensive C] : Coverage C where
  covering B := { S | âˆƒ (Î± : Type) (_ : Fintype Î±) (X : Î± â†’ C) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)),
    S = Presieve.ofArrows X Ï€ âˆ§ IsIso (Sigma.desc Ï€) }
  pullback := by
    intro X Y f S âŸ¨Î±, hÎ±, Z, Ï€, hS, h_isoâŸ©
    let Z' : Î± â†’ C := fun a â†¦ pullback f (Ï€ a)
    let Ï€' : (a : Î±) â†’ Z' a âŸ¶ Y := fun a â†¦ pullback.fst
    refine âŸ¨@Presieve.ofArrows C _ _ Î± Z' Ï€', âŸ¨?_, ?_âŸ©âŸ©
    Â· constructor
      exact âŸ¨hÎ±, Z', Ï€', âŸ¨by simp only, Extensive.sigma_desc_iso (fun x => Ï€ x) f h_isoâŸ©âŸ©
    Â· intro W g hg
      rcases hg with âŸ¨aâŸ©
      refine âŸ¨Z a, pullback.snd, Ï€ a, ?_, by rw [CategoryTheory.Limits.pullback.condition]âŸ©
      rw [hS]
      refine Presieve.ofArrows.mk a

variable {C}

lemma extensive_union_regular_generates_coherent [Regular C] [Extensive C] [Precoherent C] :
    ((extensiveCoverage C).union (regularCoverage C)).toGrothendieck =
    (coherentTopology C) := by
  ext B S
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· dsimp [Coverage.toGrothendieck] at *
    induction h with
    | of Y T hT =>
      Â· apply Coverage.saturate.of
        dsimp [coherentCoverage]
        simp only [Coverage.union, extensiveCoverage, regularCoverage, Set.mem_union,
          Set.mem_setOf_eq] at hT
        apply Or.elim hT
        <;> intro h
        Â· obtain âŸ¨Î±, x, X, Ï€, hâŸ© := h
          refine âŸ¨Î±, x, X, Ï€, âŸ¨h.1, ?_âŸ©âŸ©
          letI := h.2
          infer_instance
        Â· obtain âŸ¨Z, f, âŸ¨hh, hâŸ©âŸ©  := h
          refine âŸ¨Unit, inferInstance, (fun _ â†¦ Z), (fun _ â†¦ f), âŸ¨hh, ?_âŸ©âŸ©
          infer_instance
    | top =>
      Â· apply Coverage.saturate.top
    | transitive Y T =>
      Â· apply Coverage.saturate.transitive Y T
        Â· assumption
        Â· assumption
  Â· induction h with
    | of Y T hT =>
      Â· dsimp [coherentCoverage] at hT
        obtain âŸ¨I, hI, X, f, âŸ¨h, hTâŸ©âŸ© := hT
        let Ï† := fun (i : I) â†¦ Sigma.Î¹ X i
        let F := Sigma.desc f
        let Z := Sieve.generate T
        let Xs := (âˆ fun (i : I) => X i)
        let Zf : Sieve Y := Sieve.generate
          (Presieve.ofArrows (fun (_ : Unit) â†¦ Xs) (fun (_ : Unit) â†¦ F))
        apply Coverage.saturate.transitive Y Zf
        Â· apply Coverage.saturate.of
          simp only [Coverage.union, extensiveCoverage, regularCoverage, Set.mem_union,
            Set.mem_setOf_eq]
          right
          refine âŸ¨Xs, F, âŸ¨rfl, ?_âŸ©âŸ©
          infer_instance
        Â· intro R g hZfg
          dsimp at hZfg
          rw [Presieve.ofArrows_pUnit] at hZfg
          obtain âŸ¨W, Ïˆ, Ïƒ, âŸ¨hW, hW'âŸ©âŸ© := hZfg
          dsimp [Presieve.singleton] at hW
          induction hW
          rw [â† hW', Sieve.pullback_comp Z]
          suffices : Sieve.pullback Ïˆ ((Sieve.pullback F) Z) âˆˆ GrothendieckTopology.sieves
            ((extensiveCoverage C).union (regularCoverage C)).toGrothendieck R
          Â· exact this
          apply GrothendieckTopology.pullback_stable'
          dsimp [Coverage.toGrothendieck]
          suffices : Coverage.saturate ((extensiveCoverage C).union (regularCoverage C)) Xs
            (Z.pullback F)
          Â· exact this
          suffices : Sieve.generate (Presieve.ofArrows X Ï†) â‰¤ Z.pullback F
          Â· apply Coverage.saturate_of_superset _ this
            apply Coverage.saturate.of
            simp only [Coverage.union, extensiveCoverage, regularCoverage, Set.mem_union,
              Set.mem_setOf_eq]
            left
            refine âŸ¨I, hI, X, Ï†, âŸ¨rfl, ?_âŸ©âŸ©
            suffices : Sigma.desc Ï† = ğŸ™ _
            Â· rw [this]
              exact inferInstance
            ext
            simp only [colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app, Category.comp_id]
          intro Q q hq
          simp only [Sieve.pullback_apply, Sieve.generate_apply]
          simp only [Sieve.generate_apply] at hq
          obtain âŸ¨E, e, r, hqâŸ© := hq
          refine' âŸ¨E, e, r â‰« F, âŸ¨_, _âŸ©âŸ©
          Â· rw [h]
            induction hq.1
            dsimp
            simp only [colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app]
            exact Presieve.ofArrows.mk _
          Â· rw [â† hq.2]
            simp only [Category.assoc]
    | top =>
      Â· apply Coverage.saturate.top
    | transitive Y T =>
      Â· apply Coverage.saturate.transitive Y T
        Â· assumption
        Â· assumption

section ExtensiveSheaves

variable [Extensive C]

instance {X : C} (S : Presieve X) [S.extensive] : S.hasPullbacks where
  has_pullbacks := by
    obtain âŸ¨_, _, _, _, hS, _âŸ© := Presieve.extensive.arrows_sigma_desc_iso (R := S)
    intro _ _ f hf _ hg
    rw [hS] at hf hg
    cases' hg with b
    apply HasPullbacksOfInclusions.has_pullback f

namespace ExtensiveSheafConditionProof

lemma sigma_surjective {Î± : Type} {Z : Î± â†’ C} {X : C} (Ï€ : (a : Î±) â†’ Z a âŸ¶ X) :
    Function.Surjective (fun a => âŸ¨Z a, Ï€ a, Presieve.ofArrows.mk aâŸ© :
    Î± â†’ Î£(Y : C), { f : Y âŸ¶ X // Presieve.ofArrows Z Ï€ f }) :=
  fun âŸ¨_, âŸ¨_, hfâŸ©âŸ© â†¦ by cases' hf with a _; exact âŸ¨a, rflâŸ©

open Opposite

instance {Î± : Type} {Z : Î± â†’ C} {X : C} {Ï€ : (a : Î±) â†’ Z a âŸ¶ X} [Fintype Î±] :
    HasProduct fun (x : Î£(Y : C), { f : Y âŸ¶ X // Presieve.ofArrows Z Ï€ f }) â†¦ (op x.1) :=
  haveI := Finite.of_surjective _ (sigma_surjective Ï€)
  inferInstance

/-- The canonical map from `Equalizer.FirstObj` to a product indexed by `Î±` -/
noncomputable
def prod_map {Î± : Type} {Z : Î± â†’ C} {X : C} (Ï€ : (a : Î±) â†’ Z a âŸ¶ X) (F : Cáµ’áµ– â¥¤ Type max u v) :
    (âˆ fun (f : (Î£(Y : C), { f : Y âŸ¶ X // Presieve.ofArrows Z Ï€ f })) => F.obj (op f.fst)) âŸ¶
    âˆ fun a => F.obj (op (Z a)) :=
  Pi.lift (fun a => Pi.Ï€ _ âŸ¨Z a, Ï€ a, Presieve.ofArrows.mk aâŸ©) â‰« ğŸ™ _

/-- The inverse to `Equalizer.forkMap F (Presieve.ofArrows Z Ï€)`. -/
noncomputable
def firstObj_to_base {Î± : Type} [Fintype Î±] {Z : Î± â†’ C} {X : C} (Ï€ : (a : Î±) â†’ Z a âŸ¶ X)
  (F : Cáµ’áµ– â¥¤ Type max u v) [PreservesFiniteProducts F] [IsIso (Sigma.desc Ï€)] :
    Equalizer.FirstObj F (Presieve.ofArrows Z Ï€) âŸ¶ F.obj (op X) :=
  haveI : PreservesLimit (Discrete.functor fun a => op (Z a)) F :=
    (PreservesFiniteProducts.preserves Î±).preservesLimit
  (prod_map Ï€ F) â‰« ((Limits.PreservesProduct.iso F (fun a => op <| Z a)).inv â‰«
    F.map (opCoproductIsoProduct Z).inv â‰« F.map (inv (Sigma.desc Ï€).op))

lemma comp_inv_desc_eq_Î¹ {Î± : Type} [Fintype Î±] {Z : Î± â†’ C} {X : C} (Ï€ : (a : Î±) â†’ Z a âŸ¶ X)
    [IsIso (Sigma.desc Ï€)] (a : Î±) : Ï€ a â‰« inv (Sigma.desc Ï€) = Sigma.Î¹ _ a := by
  simp only [IsIso.comp_inv_eq, colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app]

@[simp]
lemma PreservesProduct.isoInvCompMap {C : Type u} [Category C] {D : Type v} [Category D] (F : C â¥¤ D)
    {J : Type w} {f : J â†’ C} [HasProduct f] [HasProduct (fun j => F.obj (f j))]
    [PreservesLimit (Discrete.functor f) F] (j : J) :
    (PreservesProduct.iso F f).inv â‰« F.map (Pi.Ï€ _ j) = Pi.Ï€ _ j :=
  IsLimit.conePointUniqueUpToIso_inv_comp _ (limit.isLimit _) (âŸ¨jâŸ© : Discrete J)

instance {Î± : Type} [Fintype Î±] {Z : Î± â†’ C} {F : C â¥¤ Type w}
    [PreservesFiniteProducts F] : PreservesLimit (Discrete.functor fun a => (Z a)) F :=
  (PreservesFiniteProducts.preserves Î±).preservesLimit

instance {X : C} (S : Presieve X) [S.extensive]
    {F : Cáµ’áµ– â¥¤ Type max u v} [PreservesFiniteProducts F] : IsIso (Equalizer.forkMap F S) := by
  obtain âŸ¨Î±, _, Z, Ï€, hS, _âŸ© := Presieve.extensive.arrows_sigma_desc_iso (R := S)
  subst hS
  refine' âŸ¨firstObj_to_base Ï€ F,_,_âŸ©
  Â· simp only [firstObj_to_base, â† Category.assoc, Functor.map_inv,
      IsIso.comp_inv_eq, Category.id_comp, â† Functor.mapIso_inv, Iso.comp_inv_eq,
      Functor.mapIso_hom, Iso.comp_inv_eq, â† Functor.map_comp,
      desc_op_comp_opCoproductIsoProduct_hom, PreservesProduct.iso_hom, map_lift_piComparison,
      colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app]
    funext s
    ext a
    simp only [prod_map, types_comp_apply, types_id_apply, Types.Limit.lift_Ï€_apply,
      Fan.mk_pt, Equalizer.forkMap, Fan.mk_Ï€_app, Types.pi_lift_Ï€_apply]
  Â· refine Limits.Pi.hom_ext _ _ (fun f => ?_)
    simp only [Equalizer.forkMap, Category.assoc, limit.lift_Ï€, Fan.mk_pt, Fan.mk_Ï€_app,
      Category.id_comp]
    obtain âŸ¨a, haâŸ© := sigma_surjective Ï€ f
    rw [firstObj_to_base, Category.assoc, Category.assoc, Category.assoc, â† Functor.map_comp,
      â† op_inv, â† op_comp, â† ha, comp_inv_desc_eq_Î¹, â† Functor.map_comp,
      opCoproductIsoProduct_inv_comp_Î¹, PreservesProduct.isoInvCompMap F a]
    simp only [prod_map, Category.comp_id, limit.lift_Ï€, Fan.mk_pt, Fan.mk_Ï€_app]

end ExtensiveSheafConditionProof

open ExtensiveSheafConditionProof in
lemma isSheafFor_extensive_of_preservesFiniteProducts {X : C} (S : Presieve X) [S.extensive]
    (F : Cáµ’áµ– â¥¤ Type max u v) [PreservesFiniteProducts F] :
    Presieve.IsSheafFor F S := by
  refine' (Equalizer.Presieve.sheaf_condition F S).2 _
  rw [Limits.Types.type_equalizer_iff_unique]
  dsimp [Equalizer.FirstObj]
  suffices : IsIso (Equalizer.forkMap F S)
  Â· intro y _
    refine' âŸ¨inv (Equalizer.forkMap F S) y, _, fun yâ‚ hyâ‚ => _âŸ©
    Â· change (inv (Equalizer.forkMap F S) â‰« (Equalizer.forkMap F S)) y = y
      rw [IsIso.inv_hom_id, types_id_apply]
    Â· replace hyâ‚ := congr_arg (inv (Equalizer.forkMap F S)) hyâ‚
      change ((Equalizer.forkMap F S) â‰« inv (Equalizer.forkMap F S)) _ = _ at hyâ‚
      rwa [IsIso.hom_inv_id, types_id_apply] at hyâ‚
  infer_instance

end ExtensiveSheaves

end CategoryTheory
