/-
Copyright (c) 2023 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.CategoryTheory.Functor.KanExtension.Pointwise
import Mathlib.Condensed.Functors
import Mathlib.Condensed.Limits

/-!

# Solid modules

This file contains the definition of a solid `R`-module: `CondensedMod.isSolid R`. Solid modules
groups were introduced in [scholze2019condensed], Definition 5.1.

## Main definition

* `CondensedMod.IsSolid R`: the predicate on condensed `R`-modules describing the property of
  being solid.

TODO (hard): prove that `((profiniteSolid ‚Ñ§).obj S).IsSolid` for `S : Profinite`.
TODO (slightly easier): prove that `((profiniteSolid ùîΩ‚Çö).obj S).IsSolid` for `S : Profinite`.
-/

universe u

variable (R : Type (u + 1)) [Ring R]

open CategoryTheory Limits Profinite Condensed

noncomputable section

namespace Condensed

/-- The free condensed `R`-module on a finite set. -/
abbrev finFree : FintypeCat.{u} ‚•§ CondensedMod.{u} R :=
  FintypeCat.toProfinite ‚ãô profiniteToCondensed ‚ãô free R

/-- The free condensed `R`-module on a profinite space. -/
abbrev profiniteFree : Profinite.{u} ‚•§ CondensedMod.{u} R :=
  profiniteToCondensed ‚ãô free R

/-- The functor sending a profinite space `S` to the condensed `R`-module `R[S]^\solid`. -/
def profiniteSolid : Profinite.{u} ‚•§ CondensedMod.{u} R :=
  Functor.rightKanExtension FintypeCat.toProfinite (finFree R)

/-- The natural transformation `FintypeCat.toProfinite ‚ãô profiniteSolid R ‚ü∂ finFree R`
which is part of the assertion that `profiniteSolid R` is the (pointwise) right
Kan extension of `finFree R` along `FintypeCat.toProfinite`. -/
def profiniteSolidCounit : FintypeCat.toProfinite ‚ãô profiniteSolid R ‚ü∂ finFree R :=
  Functor.rightKanExtensionCounit FintypeCat.toProfinite (finFree R)

instance : (profiniteSolid R).IsRightKanExtension (profiniteSolidCounit R) := by
  dsimp only [profiniteSolidCounit, profiniteSolid]
  infer_instance

/-- The functor `Profinite.{u} ‚•§ CondensedMod.{u} R` is a pointwise
right Kan extension of `finFree R : FintypeCat.{u} ‚•§ CondensedMod.{u} R`
along `FintypeCat.toProfinite`. -/
def profiniteSolidIsPointwiseRightKanExtension :
    (Functor.RightExtension.mk _ (profiniteSolidCounit R)).IsPointwiseRightKanExtension :=
  Functor.isPointwiseRightKanExtensionOfIsRightKanExtension _ _

/-- The natural transformation `R[S] ‚ü∂ R[S]^\solid`. -/
def profiniteSolidification : profiniteFree R ‚ü∂ profiniteSolid.{u} R :=
  (profiniteSolid R).liftOfIsRightKanExtension (profiniteSolidCounit R) _ (ùüô _)

end Condensed

/--
The predicate on condensed `R`-modules describing the property of being solid.

TODO: This is not the correct definition of solid `R`-modules for a general `R`. The correct one is as
follows: Use this to define solid modules over a¬†finite type `‚Ñ§`-algebra `R`. In particular this gives a
definition of solid modules over `‚Ñ§[X]` (polynomials in one variable). Then a solid `R`-module over a
general ring `R` is the condition that for every `r ‚àà R` and every ring homomorphism `‚Ñ§[X] ‚Üí R` such that
`X` maps to `r`, the underlying `‚Ñ§[X]`-module is solid.
-/
class CondensedMod.IsSolid (A : CondensedMod.{u} R) : Prop where
  isIso_solidification_map : ‚àÄ X : Profinite.{u}, IsIso ((yoneda.obj A).map
    ((profiniteSolidification R).app X).op)
