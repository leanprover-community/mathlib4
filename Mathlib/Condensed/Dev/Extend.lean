/-
Copyright (c) 2024 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.Topology.Category.Profinite.AsLimit
import Mathlib.Topology.Category.Profinite.CofilteredLimit
import Mathlib.CategoryTheory.Functor.KanExtension.Pointwise
import Mathlib.CategoryTheory.Filtered.Final
/-!

# Extending a functor from `FintypeCat` to `Profinite`
-/

universe u

open CategoryTheory Limits FintypeCat Functor

attribute [local instance] FintypeCat.discreteTopology ConcreteCategory.instFunLike

namespace CompHausLike

variable {I : Type u} [Category.{u} I] [IsCofiltered I] {F : I ‚•§ FintypeCat.{u}}

variable {P : TopCat.{u} ‚Üí Prop} [‚àÄ (Y : TopCat) [Fintype Y], HasProp P Y]

instance (A : FintypeCat.{u}) :
    letI _ : TopologicalSpace A := ‚ä•
    HasProp P (TopCat.of A) :=
  letI _ : TopologicalSpace A := ‚ä•
  letI _ : Fintype (TopCat.of A) := inferInstanceAs (Fintype A)
  inferInstance

-- def _root_.FintypeCat.toCompHausLike : FintypeCat ‚•§ CompHausLike P where
--   obj A :=
--     let _ : TopologicalSpace A := ‚ä•
--     have : HasProp P A := inferInstanceAs (HasProp P (TopCat.of A))
--     CompHausLike.of P A
--   map f := ‚ü®f, by continuity‚ü©

end CompHausLike

namespace Profinite

variable {I : Type u} [Category.{u} I] [IsCofiltered I] {F : I ‚•§ FintypeCat.{u}}
    (c : Cone <| F ‚ãô toProfinite)

/--
A continuous map from a profinite set to a finite set factors through one of the components of
the profinite set when written as a cofiltered limit of finite sets.
-/
lemma exists_hom (hc : IsLimit c) {X : FintypeCat} (f : c.pt ‚ü∂ toProfinite.obj X) :
    ‚àÉ (i : I) (g : F.obj i ‚ü∂ X), f = c.œÄ.app i ‚â´ toProfinite.map g := by
  let _ : TopologicalSpace X := ‚ä•
  have : DiscreteTopology (toProfinite.obj X) :=
    inferInstanceAs (DiscreteTopology X)
  let f' : LocallyConstant c.pt (toProfinite.obj X) :=
    ‚ü®f, (IsLocallyConstant.iff_continuous _).mpr f.continuous‚ü©
  obtain ‚ü®i, g, h‚ü© := Profinite.exists_locallyConstant.{_, u} c hc f'
  refine ‚ü®i, g.toFun, ?_‚ü©
  ext x
  exact LocallyConstant.congr_fun h x

namespace Extend

@[simps]
def functor : I ‚•§ StructuredArrow c.pt toProfinite where
  obj i := StructuredArrow.mk (c.œÄ.app i)
  map f := StructuredArrow.homMk (F.map f) (c.w f)

def functorOp : I·µí·µñ ‚•§ CostructuredArrow toProfinite.op ‚ü®c.pt‚ü© :=
  (functor c).op ‚ãô StructuredArrow.toCostructuredArrow _ _

example : functor c ‚ãô StructuredArrow.proj c.pt toProfinite ‚âÖ F := Iso.refl _

example : functorOp c ‚ãô CostructuredArrow.proj toProfinite.op ‚ü®c.pt‚ü© ‚âÖ F.op := Iso.refl _

theorem functor_initial (hc : IsLimit c) [‚àÄ i, Epi (c.œÄ.app i)] : Initial (functor c) := by
  rw [initial_iff_of_isCofiltered (F := functor c)]
  constructor
  ¬∑ intro ‚ü®_, X, (f : c.pt ‚ü∂ _)‚ü©
    obtain ‚ü®i, g, h‚ü© := Profinite.exists_hom c hc f
    refine ‚ü®i, ‚ü®StructuredArrow.homMk g h.symm‚ü©‚ü©
  ¬∑ intro ‚ü®_, X, (f : c.pt ‚ü∂ _)‚ü© i ‚ü®_, (s : F.obj i ‚ü∂ X), (w : f = c.œÄ.app i ‚â´ _)‚ü©
      ‚ü®_, (s' : F.obj i ‚ü∂ X), (w' : f = c.œÄ.app i ‚â´ _)‚ü©
    simp only [functor_obj, functor_map, StructuredArrow.hom_eq_iff, StructuredArrow.mk_right,
      StructuredArrow.comp_right, StructuredArrow.homMk_right]
    refine ‚ü®i, ùüô _, ?_‚ü©
    simp only [CategoryTheory.Functor.map_id, Category.id_comp]
    rw [w] at w'
    exact toProfinite.map_injective <| Epi.left_cancellation _ _ w'

theorem functorOp_final (hc : IsLimit c) [‚àÄ i, Epi (c.œÄ.app i)] : Final (functorOp c) := by
  have := functor_initial c hc
  have : ((StructuredArrow.toCostructuredArrow toProfinite c.pt)).IsEquivalence  :=
    (inferInstance : (structuredArrowOpEquivalence _ _).functor.IsEquivalence )
  exact Functor.final_comp (functor c).op _

section Limit

variable {C : Type*} [Category C] (G : Profinite ‚•§ C)

def cone (S : Profinite) :
    Cone (StructuredArrow.proj S toProfinite ‚ãô toProfinite ‚ãô G) where
  pt := G.obj S
  œÄ := {
    app := fun i ‚Ü¶ G.map i.hom
    naturality := fun _ _ f ‚Ü¶ (by
      have := f.w
      simp only [const_obj_obj, StructuredArrow.left_eq_id, const_obj_map, Category.id_comp,
        StructuredArrow.w] at this
      simp only [const_obj_obj, comp_obj, StructuredArrow.proj_obj, const_obj_map, Category.id_comp,
        Functor.comp_map, StructuredArrow.proj_map, ‚Üê map_comp, StructuredArrow.w]) }

example : G.mapCone c = (cone G c.pt).whisker (functor c) := rfl

noncomputable
def isLimitCone (hc : IsLimit c) [‚àÄ i, Epi (c.œÄ.app i)] (hc' : IsLimit <| G.mapCone c) :
    IsLimit (cone G c.pt) := (functor_initial c hc).isLimitWhiskerEquiv _ hc'

end Limit

section Colimit

variable {C : Type*} [Category C] (G : Profinite·µí·µñ ‚•§ C)

@[simps]
def cocone (S : Profinite) :
    Cocone (CostructuredArrow.proj toProfinite.op ‚ü®S‚ü© ‚ãô toProfinite.op ‚ãô G) where
  pt := G.obj ‚ü®S‚ü©
  Œπ := {
    app := fun i ‚Ü¶ G.map i.hom
    naturality := fun _ _ f ‚Ü¶ (by
      have := f.w
      simp only [op_obj, const_obj_obj, op_map, CostructuredArrow.right_eq_id, const_obj_map,
        Category.comp_id] at this
      simp only [comp_obj, CostructuredArrow.proj_obj, op_obj, const_obj_obj, Functor.comp_map,
        CostructuredArrow.proj_map, op_map, ‚Üê map_comp, this, const_obj_map, Category.comp_id]) }

example : G.mapCocone c.op = (cocone G c.pt).whisker (functorOp c) := rfl

noncomputable
def isColimitCocone (hc : IsLimit c) [‚àÄ i, Epi (c.œÄ.app i)] (hc' : IsColimit <| G.mapCocone c.op) :
    IsColimit (cocone G c.pt) := (functorOp_final c hc).isColimitWhiskerEquiv _ hc'

end Colimit

end Extend

open Extend

section ProfiniteAsLimit

variable (S : Profinite)

abbrev diagram' : StructuredArrow S toProfinite ‚•§ Profinite :=
  StructuredArrow.proj S toProfinite ‚ãô toProfinite

def asLimitCone' : Cone (S.diagram') := cone (ùü≠ _) S

instance (i : DiscreteQuotient S) : Epi (S.asLimitCone.œÄ.app i) :=
  (epi_iff_surjective _).mpr i.proj_surjective

noncomputable def asLimit' : IsLimit S.asLimitCone' := isLimitCone _ (ùü≠ _) S.asLimit S.asLimit

noncomputable def lim' : LimitCone S.diagram' := ‚ü®S.asLimitCone', S.asLimit'‚ü©

end ProfiniteAsLimit

end Profinite
