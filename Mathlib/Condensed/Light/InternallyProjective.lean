/-
Copyright (c) 2025 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
module

public import Mathlib.CategoryTheory.Preadditive.Projective.Internal
public import Mathlib.Condensed.Light.Epi
public import Mathlib.Condensed.Light.Functors
public import Mathlib.Condensed.Light.Monoidal
/-!

# Characterization of internal projectivity in light condensed modules

This file gives an explicit condition on light condensed modules over a ring `R` to be internally
projective, namely the following:

`internallyProjective_iff_tensor_condition`: `P : LightCondMod R` is internally projective if and
only if, for all `A B : LightCondMod R`, for all epimorphisms `e : A âŸ¶ B`, for all
`S : LightProfinite` and all morphisms `g : P âŠ— R[S] âŸ¶ B`, there exists a `S' : LightProfinite`
with a surjeciton `Ï€ : S' âŸ¶ S` and a morphism `g' : P âŠ— R[S'] âŸ¶ A`, making the diagram
```
P âŠ— R[S'] --> A
  |           |
  v           v
P âŠ— R[S]  --> B
```
commute.

We also provide the analogous characterization with the tensor product commuted the other way around
(see `internallyProjective_iff_tensor_condition'`), and the special cases when `P` is the free
condensed module on a condensed set (`free_internallyProjective_iff_tensor_condition`,
`free_internallyProjective_iff_tensor_condition'`) and when `P` is the free condensed module on a
light profinite set (`free_lightProfinite_internallyProjective_iff_tensor_condition`/
`free_lightProfinite_internallyProjective_iff_tensor_condition'`).
-/

@[expose] public section

universe u

open CategoryTheory Category MonoidalCategory Functor Monoidal LaxMonoidal OplaxMonoidal

variable (R : Type u) [CommRing R]

namespace LightCondensed

/--
The `S`-valued points of the internal hom `A âŸ¶[LightCondMod R] B` are in bijection with
morpisms `A âŠ— R[S] âŸ¶ B`.
-/
noncomputable def ihomPoints (A B : LightCondMod.{u} R) (S : LightProfinite) :
    (A âŸ¶[LightCondMod R] B).val.obj âŸ¨SâŸ© â‰ƒ ((A âŠ— ((free R).obj S.toCondensed)) âŸ¶ B) :=
  (((freeForgetAdjunction R).homEquiv _ _).trans
    (coherentTopology _).yonedaEquiv).symm.trans
      ((ihom.adjunction A).homEquiv _ _).symm

lemma ihomPoints_apply (A B : LightCondMod.{u} R) (S : LightProfinite)
    (x : (A âŸ¶[LightCondMod R] B).val.obj âŸ¨SâŸ©) :
    ihomPoints R A B S x = (MonoidalClosed.uncurry (((freeForgetAdjunction R).homEquiv _ _).symm
      ((coherentTopology LightProfinite.{u}).yonedaEquiv.symm x))) :=
  rfl

lemma ihomPoints_symm_apply (A B : LightCondMod.{u} R) (S : LightProfinite)
    (x : (A âŠ— ((free R).obj S.toCondensed)) âŸ¶ B) :
    (ihomPoints R A B S).symm x = (coherentTopology LightProfinite.{u}).yonedaEquiv
      ((freeForgetAdjunction R).homEquiv _ _ (MonoidalClosed.curry x)) :=
  rfl

lemma ihom_map_val_app (A B P : LightCondMod.{u} R) (S : LightProfinite) (e : A âŸ¶ B)
    (x : (P âŸ¶[LightCondMod R] A).val.obj âŸ¨SâŸ©) :
    (((ihom P).map e).val.app âŸ¨SâŸ©) x = (ihomPoints R P B S).symm (ihomPoints R P A S x â‰« e) := by
  apply (ihomPoints R P B S).injective
  simp only [ihomPoints_apply, Equiv.apply_symm_apply, â† MonoidalClosed.uncurry_natural_right,
    â† Adjunction.homEquiv_naturality_right_symm]
  congr
  cat_disch

lemma ihomPoints_symm_comp (B P : LightCondMod.{u} R) (S S' : LightProfinite) (Ï€ : S âŸ¶ S')
    (f : P âŠ— (free R).obj S'.toCondensed âŸ¶ B) :
    (ihomPoints R P B S).symm (P â— (free R).map (lightProfiniteToLightCondSet.map Ï€) â‰« f) =
      ((P âŸ¶[LightCondMod R] B).val.map Ï€.op) ((ihomPoints R P B S').symm f) := by
  have : (lightProfiniteToLightCondSet.map Ï€).val.app (Opposite.op S) (ğŸ™ S) =
      S'.toCondensed.val.map Ï€.op (ğŸ™ S') := rfl
  simp [ihomPoints_symm_apply, MonoidalClosed.curry_natural_left, Adjunction.homEquiv_apply,
    GrothendieckTopology.yonedaEquiv_apply, this]
  rfl

/--
`P : LightCondMod R` is internally projective if and
only if, for all `A B : LightCondMod R`, for all epimorphisms `e : A âŸ¶ B`, for all
`S : LightProfinite` and all morphisms `g : P âŠ— R[S] âŸ¶ B`, there exists a `S' : LightProfinite`
with a surjeciton `Ï€ : S' âŸ¶ S` and a morphism `g' : P âŠ— R[S'] âŸ¶ A`, making the diagram
```
P âŠ— R[S'] --> A
  |           |
  v           v
P âŠ— R[S]  --> B
```
commute.
-/
lemma internallyProjective_iff_tensor_condition (P : LightCondMod R) : InternallyProjective P â†”
    âˆ€ {A B : LightCondMod R} (e : A âŸ¶ B) [Epi e],
      (âˆ€ (S : LightProfinite) (g : P âŠ— (free R).obj S.toCondensed âŸ¶ B), âˆƒ (S' : LightProfinite)
        (Ï€ : S' âŸ¶ S) (_ : Function.Surjective Ï€) (g' : P âŠ— (free R).obj S'.toCondensed âŸ¶ A),
          (P â— ((lightProfiniteToLightCondSet â‹™ free R).map Ï€)) â‰« g = g' â‰« e) := by
  refine âŸ¨fun âŸ¨hâŸ© A B e he S g â†¦ ?_, fun h â†¦ âŸ¨âŸ¨fun {A B} e he â†¦ ?_âŸ©âŸ©âŸ©
  Â· have hh := h.1 e
    rw [LightCondMod.epi_iff_locallySurjective_on_lightProfinite] at hh
    specialize hh S ((ihomPoints R P B S).symm g)
    obtain âŸ¨S', Ï€, hÏ€, g', hhâŸ© := hh
    refine âŸ¨S', Ï€, hÏ€, (ihomPoints _ _ _ _) g', ?_âŸ©
    rw [ihom_map_val_app] at hh
    apply (ihomPoints R P B S').symm.injective
    rw [hh]
    exact ihomPoints_symm_comp R B P S' S Ï€ g
  Â· rw [LightCondMod.epi_iff_locallySurjective_on_lightProfinite]
    intro S g
    specialize h e S ((ihomPoints _ _ _ _) g)
    obtain âŸ¨S', Ï€, hÏ€, g', hhâŸ© := h
    refine âŸ¨S', Ï€, hÏ€, (ihomPoints _ _ _ _).symm g', ?_âŸ©
    rw [ihom_map_val_app]
    have := ihomPoints_symm_comp R B P S' S Ï€ ((ihomPoints R P B S) g)
    dsimp at hh
    rw [hh] at this
    simp [this, Quiver.Hom.op]

/--
`P : LightCondMod R` is internally projective if and
only if, for all `A B : LightCondMod R`, for all epimorphisms `e : A âŸ¶ B`, for all
`S : LightProfinite` and all morphisms `g : R[S] âŠ— P âŸ¶ B`, there exists a `S' : LightProfinite`
with a surjeciton `Ï€ : S' âŸ¶ S` and a morphism `g' : R[S'] âŠ— P âŸ¶ A`, making the diagram
```
R[S'] âŠ— P --> A
  |           |
  v           v
R[S] âŠ— P  --> B
```
commute.
-/
lemma internallyProjective_iff_tensor_condition' (P : LightCondMod R) : InternallyProjective P â†”
    âˆ€ {A B : LightCondMod R} (e : A âŸ¶ B) [Epi e],
      (âˆ€ (S : LightProfinite) (g : (free R).obj S.toCondensed âŠ— P âŸ¶ B), âˆƒ (S' : LightProfinite)
        (Ï€ : S' âŸ¶ S) (_ : Function.Surjective Ï€) (g' : (free R).obj S'.toCondensed âŠ— P âŸ¶ A),
          (((lightProfiniteToLightCondSet â‹™ free R).map Ï€) â–· P) â‰« g = g' â‰« e) := by
  rw [internallyProjective_iff_tensor_condition]
  refine âŸ¨fun h A B e he S g â†¦ ?_, fun h A B e he S g â†¦ ?_âŸ©
  Â· specialize h e S ((Î²_ _ _).hom â‰« g)
    obtain âŸ¨S', Ï€, hÏ€, g', hhâŸ© := h
    refine âŸ¨S', Ï€, hÏ€, (Î²_ _ _).inv â‰« g', ?_âŸ©
    simp [â† hh]
  Â· specialize h e S ((Î²_ _ _).inv â‰« g)
    obtain âŸ¨S', Ï€, hÏ€, g', hhâŸ© := h
    refine âŸ¨S', Ï€, hÏ€, (Î²_ _ _).hom â‰« g', ?_âŸ©
    simp [â† hh]

/--
Given a `P : LightCondSet`, the light free light condensed module `R[P]` is internally projective if
and only if, for all `A B : LightCondMod R`, for all epimorphisms `e : A âŸ¶ B`, for all
`S : LightProfinite` and all morphisms `g : R[P Ã— S] âŸ¶ B`, there exists a `S' : LightProfinite`
with a surjeciton `Ï€ : S' âŸ¶ S` and a morphism `g' : R[P Ã— S'] âŸ¶ A`, making the diagram
```
R[P Ã— S'] --> A
  |           |
  v           v
R[P Ã— S]  --> B
```
commute.
-/
lemma free_internallyProjective_iff_tensor_condition (P : LightCondSet.{u}) :
    InternallyProjective ((free R).obj P) â†”
      âˆ€ {A B : LightCondMod R} (e : A âŸ¶ B) [Epi e], (âˆ€ (S : LightProfinite)
        (g : (free R).obj (P âŠ— S.toCondensed) âŸ¶ B), âˆƒ (S' : LightProfinite)
          (Ï€ : S' âŸ¶ S) (_ : Function.Surjective Ï€) (g' : (free R).obj (P âŠ— S'.toCondensed) âŸ¶ A),
            ((free R).map (P â— ((lightProfiniteToLightCondSet).map Ï€))) â‰« g = g' â‰« e) := by
  rw [internallyProjective_iff_tensor_condition]
  refine âŸ¨fun h A B e he S g â†¦ ?_, fun h A B e he S g â†¦ ?_âŸ©
  Â· specialize h e S ((Î¼Iso (free R) _ _).hom â‰« g)
    obtain âŸ¨S', Ï€, hÏ€, g', hhâŸ© := h
    refine âŸ¨S', Ï€, hÏ€, (Î¼Iso (free R) _ _).inv â‰« g', ?_âŸ©
    rw [assoc, â† hh]
    simp only [â† assoc]
    -- Generated by `simp?`. Leaving it unsqueezed is too slow
    simp only [Î¼Iso_hom, Î¼Iso_inv, Functor.comp_map, Î´_natural_right, assoc, Î´_Î¼, comp_id]
  Â· specialize h e S ((Î¼Iso (free R) _ _).inv â‰« g)
    obtain âŸ¨S', Ï€, hÏ€, g', hhâŸ© := h
    refine âŸ¨S', Ï€, hÏ€, (Î¼Iso (free R) _ _).hom â‰« g', ?_âŸ©
    rw [assoc, â† hh, â† assoc, â† assoc]
    -- Generated by `simp? [â† Î¼_natural_right]`.
    -- Leaving it unsqueezed is too slow
    simp only [comp_obj, Functor.comp_map, Î¼Iso_hom, â† Î¼_natural_right, Î¼Iso_inv, assoc, Î¼_Î´,
      comp_id]

/--
Given a `P : LightCondSet`, the light free light condensed module `R[P]` is internally projective if
and only if, for all `A B : LightCondMod R`, for all epimorphisms `e : A âŸ¶ B`, for all
`S : LightProfinite` and all morphisms `g : R[S Ã— P] âŸ¶ B`, there exists a `S' : LightProfinite`
with a surjeciton `Ï€ : S' âŸ¶ S` and a morphism `g' : R[S' Ã— P] âŸ¶ A`, making the diagram
```
R[S' Ã— P] --> A
  |           |
  v           v
R[S Ã— P]  --> B
```
commute.
-/
lemma free_internallyProjective_iff_tensor_condition' (P : LightCondSet.{u}) :
    InternallyProjective ((free R).obj P) â†”
      âˆ€ {A B : LightCondMod R} (e : A âŸ¶ B) [Epi e], (âˆ€ (S : LightProfinite)
        (g : (free R).obj (S.toCondensed âŠ— P) âŸ¶ B), âˆƒ (S' : LightProfinite)
          (Ï€ : S' âŸ¶ S) (_ : Function.Surjective Ï€) (g' : (free R).obj (S'.toCondensed âŠ— P) âŸ¶ A),
            ((free R).map (((lightProfiniteToLightCondSet).map Ï€) â–· P)) â‰« g = g' â‰« e) := by
  rw [internallyProjective_iff_tensor_condition']
  refine âŸ¨fun h A B e he S g â†¦ ?_, fun h A B e he S g â†¦ ?_âŸ©
  Â· specialize h e S ((Î¼Iso (free R) _ _).hom â‰« g)
    obtain âŸ¨S', Ï€, hÏ€, g', hhâŸ© := h
    refine âŸ¨S', Ï€, hÏ€, (Î¼Iso (free R) _ _).inv â‰« g', ?_âŸ©
    rw [assoc, â† hh]
    -- Generated by `simp?`. Leaving it unsqueezed is too slow
    simp only [Î¼Iso_inv, comp_obj, Functor.comp_map, Î¼Iso_hom, Î¼_natural_left_assoc, Î´_Î¼_assoc]
  Â· specialize h e S ((Î¼Iso (free R) _ _).inv â‰« g)
    obtain âŸ¨S', Ï€, hÏ€, g', hhâŸ© := h
    refine âŸ¨S', Ï€, hÏ€, (Î¼Iso (free R) _ _).hom â‰« g', ?_âŸ©
    rw [assoc, â† hh, â† assoc, â† assoc]
    -- Generated by `simp? [â† Î¼_natural_left]`
    -- Leaving it unsqueezed is too slow.
    simp only [comp_obj, Functor.comp_map, Î¼Iso_hom, â† Î¼_natural_left, Î¼Iso_inv, assoc, Î¼_Î´,
      comp_id]

/--
Given a `P : LightProfinite`, the light free light condensed module `R[P]` is internally projective
if and only if, for all `A B : LightCondMod R`, for all epimorphisms `e : A âŸ¶ B`, for all
`S : LightProfinite` and all morphisms `g : R[P Ã— S] âŸ¶ B`, there exists a `S' : LightProfinite`
with a surjeciton `Ï€ : S' âŸ¶ S` and a morphism `g' : R[P Ã— S'] âŸ¶ A`, making the diagram
```
R[P Ã— S'] --> A
  |           |
  v           v
R[P Ã— S]  --> B
```
commute.
-/
lemma free_lightProfinite_internallyProjective_iff_tensor_condition (P : LightProfinite.{u}) :
    InternallyProjective ((free R).obj P.toCondensed) â†”
      âˆ€ {A B : LightCondMod R} (e : A âŸ¶ B) [Epi e], (âˆ€ (S : LightProfinite)
        (g : (free R).obj ((P âŠ— S).toCondensed) âŸ¶ B), âˆƒ (S' : LightProfinite)
          (Ï€ : S' âŸ¶ S) (_ : Function.Surjective Ï€) (g' : (free R).obj (P âŠ— S').toCondensed âŸ¶ A),
            ((free R).map (lightProfiniteToLightCondSet.map (P â— Ï€))) â‰« g = g' â‰« e) := by
  rw [free_internallyProjective_iff_tensor_condition]
  refine âŸ¨fun h A B e he S g â†¦ ?_, fun h A B e he S g â†¦ ?_âŸ©
  Â· specialize h e S ((free R).map (Î¼Iso lightProfiniteToLightCondSet _ _).hom â‰« g)
    obtain âŸ¨S', Ï€, hÏ€, g', hhâŸ© := h
    refine âŸ¨S', Ï€, hÏ€, (free R).map (Î¼Iso
        lightProfiniteToLightCondSet _ _).inv â‰« g', ?_âŸ©
    rw [assoc, â† hh]
    simp [-map_comp, â† map_comp_assoc]
  Â· specialize h e S ((free R).map (Î¼Iso lightProfiniteToLightCondSet _ _).inv â‰« g)
    obtain âŸ¨S', Ï€, hÏ€, g', hhâŸ© := h
    refine âŸ¨S', Ï€, hÏ€, (free R).map
      (Î¼Iso lightProfiniteToLightCondSet _ _).hom â‰« g', ?_âŸ©
    rw [assoc, â† hh]
    simp [-map_comp, â† map_comp_assoc, â† Î¼_natural_right_assoc]

/--
Given a `P : LightProfinite`, the light free light condensed module `R[P]` is internally projective
if and only if, for all `A B : LightCondMod R`, for all epimorphisms `e : A âŸ¶ B`, for all
`S : LightProfinite` and all morphisms `g : R[S Ã— P] âŸ¶ B`, there exists a `S' : LightProfinite`
with a surjeciton `Ï€ : S' âŸ¶ S` and a morphism `g' : R[S' Ã— P] âŸ¶ A`, making the diagram
```
R[S' Ã— P] --> A
  |           |
  v           v
R[S Ã— P]  --> B
```
commute.
-/
lemma free_lightProfinite_internallyProjective_iff_tensor_condition' (P : LightProfinite.{u}) :
    InternallyProjective ((free R).obj P.toCondensed) â†”
      âˆ€ {A B : LightCondMod R} (e : A âŸ¶ B) [Epi e], (âˆ€ (S : LightProfinite)
        (g : (free R).obj ((S âŠ— P).toCondensed) âŸ¶ B), âˆƒ (S' : LightProfinite)
          (Ï€ : S' âŸ¶ S) (_ : Function.Surjective Ï€) (g' : (free R).obj (S' âŠ— P).toCondensed âŸ¶ A),
            ((free R).map (lightProfiniteToLightCondSet.map (Ï€ â–· P))) â‰« g = g' â‰« e) := by
  rw [free_internallyProjective_iff_tensor_condition']
  refine âŸ¨fun h A B e he S g â†¦ ?_, fun h A B e he S g â†¦ ?_âŸ©
  Â· specialize h e S ((free R).map (Î¼Iso lightProfiniteToLightCondSet _ _).hom â‰« g)
    obtain âŸ¨S', Ï€, hÏ€, g', hhâŸ© := h
    refine âŸ¨S', Ï€, hÏ€, (free R).map (Î¼Iso
        lightProfiniteToLightCondSet _ _).inv â‰« g', ?_âŸ©
    rw [assoc, â† hh]
    simp [-map_comp, â† map_comp_assoc]
  Â· specialize h e S ((free R).map (Î¼Iso lightProfiniteToLightCondSet _ _).inv â‰« g)
    obtain âŸ¨S', Ï€, hÏ€, g', hhâŸ© := h
    refine âŸ¨S', Ï€, hÏ€, (free R).map
      (Î¼Iso lightProfiniteToLightCondSet _ _).hom â‰« g', ?_âŸ©
    rw [assoc, â† hh]
    simp [-map_comp, â† map_comp_assoc, â† Î¼_natural_left_assoc]

end LightCondensed
