/-
Copyright (c) 2025 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.CategoryTheory.Preadditive.Projective.Internal
import Mathlib.Condensed.Light.Epi
import Mathlib.Condensed.Light.Functors
import Mathlib.Condensed.Light.Monoidal
/-!

# Characterization of internal projectivity in light condensed modules

This file gives an explicit condition on light condensed modules over a ring `R` to be internally
projective, namely the following:

`internallyProjective_iff_tensor_condition`: `P : LightCondMod R` is internally projective if and
only if, for all `A B : LightCondMod R`, for all epimorphisms `e : A ‚ü∂ B`, for all
`S : LightProfinite` and all morphisms `g : P ‚äó R[S] ‚ü∂ B`, there exists a `S' : LightProfinite`
with a surjeciton `œÄ : S' ‚ü∂ S` and a morphism `g' : P ‚äó R[S'] ‚ü∂ A`, making the diagram
```
P ‚äó R[S'] ‚ü∂ A
  |          |
  v          v
P ‚äó R[S]  ‚ü∂ B
```
commute.

We also provide the analogous characterization with the tensor product commuted the other way around
(see `internallyProjective_iff_tensor_condition'`), and the special cases when `P` is the free
condensed module on a condensed set (`free_internallyProjective_iff_tensor_condition`,
`free_internallyProjective_iff_tensor_condition'`) and when `P` is the free condensed module on a
light profinite set (`free_lightProfinite_internallyProjective_iff_tensor_condition`/
`free_lightProfinite_internallyProjective_iff_tensor_condition'`).
-/

universe u

open CategoryTheory MonoidalCategory

variable (R : Type u) [CommRing R]

namespace LightCondensed

/--
The `S`-valued points of the internal hom `A ‚ü∂[LightCondMod R] B` are in bijection with
morpisms `A ‚äó R[S] ‚ü∂ B`.
-/
noncomputable def ihomPoints (A B : LightCondMod.{u} R) (S : LightProfinite) :
    (A ‚ü∂[LightCondMod R] B).val.obj ‚ü®S‚ü© ‚âÉ ((A ‚äó ((free R).obj S.toCondensed)) ‚ü∂ B) :=
  (((freeForgetAdjunction R).homEquiv _ _).trans
    (coherentTopology _).yonedaEquiv).symm.trans
      ((ihom.adjunction A).homEquiv _ _).symm

lemma ihomPoints_apply (A B : LightCondMod.{u} R) (S : LightProfinite)
    (x : (A ‚ü∂[LightCondMod R] B).val.obj ‚ü®S‚ü©) :
    ihomPoints R A B S x = (MonoidalClosed.uncurry (((freeForgetAdjunction R).homEquiv _ _).symm
      ((coherentTopology LightProfinite.{u}).yonedaEquiv.symm x))) :=
  rfl

lemma ihomPoints_symm_apply (A B : LightCondMod.{u} R) (S : LightProfinite)
    (x : (A ‚äó ((free R).obj S.toCondensed)) ‚ü∂ B) :
    (ihomPoints R A B S).symm x = (coherentTopology LightProfinite.{u}).yonedaEquiv
      ((freeForgetAdjunction R).homEquiv _ _ (MonoidalClosed.curry x)) := by
  rfl

lemma ihom_map_val_app (A B P : LightCondMod.{u} R) (S : LightProfinite) (e : A ‚ü∂ B)
    (x : (P ‚ü∂[LightCondMod R] A).val.obj ‚ü®S‚ü©) :
    (((ihom P).map e).val.app ‚ü®S‚ü©) x = (ihomPoints R P B S).symm (ihomPoints R P A S x ‚â´ e) := by
  apply (ihomPoints R P B S).injective
  simp only [ihomPoints_apply, Equiv.apply_symm_apply]
  rw [‚Üê MonoidalClosed.uncurry_natural_right, ‚Üê Adjunction.homEquiv_naturality_right_symm]
  congr
  ext
  simp
  rfl

lemma ihomPoints_symm_comp (B P : LightCondMod.{u} R) (S S' : LightProfinite) (œÄ : S ‚ü∂ S')
    (f : P ‚äó (free R).obj S'.toCondensed ‚ü∂ B) :
    (ihomPoints R P B S).symm (P ‚óÅ (free R).map (lightProfiniteToLightCondSet.map œÄ) ‚â´ f) =
      ((P ‚ü∂[LightCondMod R] B).val.map œÄ.op) ((ihomPoints R P B S').symm f) := by
  simp only [ihomPoints_symm_apply, MonoidalClosed.curry_natural_left, Adjunction.homEquiv_apply,
    Functor.comp_obj, Functor.map_comp, Adjunction.unit_naturality_assoc]
  rw [GrothendieckTopology.yonedaEquiv_comp, GrothendieckTopology.yonedaEquiv_comp,
    GrothendieckTopology.yonedaEquiv_apply, GrothendieckTopology.yonedaEquiv_apply]
  have : (lightProfiniteToLightCondSet.map œÄ).val.app (Opposite.op S) (ùüô S) =
      S'.toCondensed.val.map œÄ.op (ùüô S') := rfl
  rw [this]
  simp
  rfl

/--
`P : LightCondMod R` is internally projective if and
only if, for all `A B : LightCondMod R`, for all epimorphisms `e : A ‚ü∂ B`, for all
`S : LightProfinite` and all morphisms `g : P ‚äó R[S] ‚ü∂ B`, there exists a `S' : LightProfinite`
with a surjeciton `œÄ : S' ‚ü∂ S` and a morphism `g' : P ‚äó R[S'] ‚ü∂ A`, making the diagram
```
P ‚äó R[S'] ‚ü∂ A
  |          |
  v          v
P ‚äó R[S]  ‚ü∂ B
```
commute.
-/
lemma internallyProjective_iff_tensor_condition (P : LightCondMod R) : InternallyProjective P ‚Üî
    ‚àÄ {A B : LightCondMod R} (e : A ‚ü∂ B) [Epi e],
      (‚àÄ (S : LightProfinite) (g : P ‚äó (free R).obj S.toCondensed ‚ü∂ B), ‚àÉ (S' : LightProfinite)
        (œÄ : S' ‚ü∂ S) (_ : Function.Surjective œÄ) (g' : P ‚äó (free R).obj S'.toCondensed ‚ü∂ A),
          (P ‚óÅ ((lightProfiniteToLightCondSet ‚ãô free R).map œÄ)) ‚â´ g = g' ‚â´ e) := by
  refine ‚ü®fun ‚ü®h‚ü© A B e he S g ‚Ü¶ ?_, fun h ‚Ü¶ ‚ü®‚ü®fun {A B} e he ‚Ü¶ ?_‚ü©‚ü©‚ü©
  ¬∑ have hh := h.1 e
    rw [LightCondMod.epi_iff_locallySurjective_on_lightProfinite] at hh
    specialize hh S ((ihomPoints R P B S).symm g)
    obtain ‚ü®S', œÄ, hœÄ, g', hh‚ü© := hh
    refine ‚ü®S', œÄ, hœÄ, (ihomPoints _ _ _ _) g', ?_‚ü©
    rw [ihom_map_val_app] at hh
    apply (ihomPoints R P B S').symm.injective
    rw [hh]
    exact ihomPoints_symm_comp R B P S' S œÄ g
  ¬∑ rw [LightCondMod.epi_iff_locallySurjective_on_lightProfinite]
    intro S g
    specialize h e S ((ihomPoints _ _ _ _) g)
    obtain ‚ü®S', œÄ, hœÄ, g', hh‚ü© := h
    refine ‚ü®S', œÄ, hœÄ, (ihomPoints _ _ _ _).symm g', ?_‚ü©
    rw [ihom_map_val_app]
    have := ihomPoints_symm_comp R B P S' S œÄ ((ihomPoints R P B S) g)
    dsimp at hh
    rw [hh] at this
    simp [this]
    rfl

/--
`P : LightCondMod R` is internally projective if and
only if, for all `A B : LightCondMod R`, for all epimorphisms `e : A ‚ü∂ B`, for all
`S : LightProfinite` and all morphisms `g : R[S] ‚äó P ‚ü∂ B`, there exists a `S' : LightProfinite`
with a surjeciton `œÄ : S' ‚ü∂ S` and a morphism `g' : R[S'] ‚äó P ‚ü∂ A`, making the diagram
```
R[S'] ‚äó P ‚ü∂ A
  |          |
  v          v
R[S] ‚äó P  ‚ü∂ B
```
commute.
-/
lemma internallyProjective_iff_tensor_condition' (P : LightCondMod R) : InternallyProjective P ‚Üî
    ‚àÄ {A B : LightCondMod R} (e : A ‚ü∂ B) [Epi e],
      (‚àÄ (S : LightProfinite) (g : (free R).obj S.toCondensed ‚äó P ‚ü∂ B), ‚àÉ (S' : LightProfinite)
        (œÄ : S' ‚ü∂ S) (_ : Function.Surjective œÄ) (g' : (free R).obj S'.toCondensed ‚äó P ‚ü∂ A),
          (((lightProfiniteToLightCondSet ‚ãô free R).map œÄ) ‚ñ∑ P) ‚â´ g = g' ‚â´ e) := by
  rw [internallyProjective_iff_tensor_condition]
  refine ‚ü®fun h A B e he S g ‚Ü¶ ?_, fun h A B e he S g ‚Ü¶ ?_‚ü©
  ¬∑ specialize h e S ((Œ≤_ _ _).hom ‚â´ g)
    obtain ‚ü®S', œÄ, hœÄ, g', hh‚ü© := h
    refine ‚ü®S', œÄ, hœÄ, (Œ≤_ _ _).inv ‚â´ g', ?_‚ü©
    simp [‚Üê hh]
  ¬∑ specialize h e S ((Œ≤_ _ _).inv ‚â´ g)
    obtain ‚ü®S', œÄ, hœÄ, g', hh‚ü© := h
    refine ‚ü®S', œÄ, hœÄ, (Œ≤_ _ _).hom ‚â´ g', ?_‚ü©
    simp [‚Üê hh]

/--
Given a `P : LightCondSet`, the light free light condensed module `R[P]` is internally projective if
and only if, for all `A B : LightCondMod R`, for all epimorphisms `e : A ‚ü∂ B`, for all
`S : LightProfinite` and all morphisms `g : R[P √ó S] ‚ü∂ B`, there exists a `S' : LightProfinite`
with a surjeciton `œÄ : S' ‚ü∂ S` and a morphism `g' : R[P √ó S'] ‚ü∂ A`, making the diagram
```
R[P √ó S'] ‚ü∂ A
  |          |
  v          v
R[P √ó S]  ‚ü∂ B
```
commute.
-/
lemma free_internallyProjective_iff_tensor_condition (P : LightCondSet.{u}) :
    InternallyProjective ((free R).obj P) ‚Üî
      ‚àÄ {A B : LightCondMod R} (e : A ‚ü∂ B) [Epi e], (‚àÄ (S : LightProfinite)
        (g : (free R).obj (P ‚äó S.toCondensed) ‚ü∂ B), ‚àÉ (S' : LightProfinite)
          (œÄ : S' ‚ü∂ S) (_ : Function.Surjective œÄ) (g' : (free R).obj (P ‚äó  S'.toCondensed) ‚ü∂ A),
            ((free R).map (P ‚óÅ ((lightProfiniteToLightCondSet).map œÄ))) ‚â´ g = g' ‚â´ e) := by
  rw [internallyProjective_iff_tensor_condition]
  refine ‚ü®fun h A B e he S g ‚Ü¶ ?_, fun h A B e he S g ‚Ü¶ ?_‚ü©
  ¬∑ specialize h e S ((Functor.Monoidal.ŒºIso (free R) _ _).hom ‚â´ g)
    obtain ‚ü®S', œÄ, hœÄ, g', hh‚ü© := h
    refine ‚ü®S', œÄ, hœÄ, (Functor.Monoidal.ŒºIso (free R) _ _).inv ‚â´ g', ?_‚ü©
    rw [Category.assoc, ‚Üê hh]
    simp only [‚Üê Category.assoc]
    -- Generated by `simp?`. Leaving it unsqueezed is too slow
    simp only [Functor.Monoidal.ŒºIso_hom, Functor.Monoidal.ŒºIso_inv,
      Functor.comp_map, Functor.OplaxMonoidal.Œ¥_natural_right,
      Category.assoc, Functor.Monoidal.Œ¥_Œº, Category.comp_id]
  ¬∑ specialize h e S ((Functor.Monoidal.ŒºIso (free R) _ _).inv ‚â´ g)
    obtain ‚ü®S', œÄ, hœÄ, g', hh‚ü© := h
    refine ‚ü®S', œÄ, hœÄ, (Functor.Monoidal.ŒºIso (free R) _ _).hom ‚â´ g', ?_‚ü©
    rw [Category.assoc, ‚Üê hh, ‚Üê Category.assoc, ‚Üê Category.assoc]
    -- Generated by `simp? [‚Üê Functor.LaxMonoidal.Œº_natural_right]`.
    -- Leaving it unsqueezed is too slow
    simp only [Functor.comp_obj, Functor.comp_map, Functor.Monoidal.ŒºIso_hom,
      ‚Üê Functor.LaxMonoidal.Œº_natural_right, Functor.Monoidal.ŒºIso_inv, Category.assoc,
      Functor.Monoidal.Œº_Œ¥, Category.comp_id]

/--
Given a `P : LightCondSet`, the light free light condensed module `R[P]` is internally projective if
and only if, for all `A B : LightCondMod R`, for all epimorphisms `e : A ‚ü∂ B`, for all
`S : LightProfinite` and all morphisms `g : R[S √ó P] ‚ü∂ B`, there exists a `S' : LightProfinite`
with a surjeciton `œÄ : S' ‚ü∂ S` and a morphism `g' : R[S' √ó P] ‚ü∂ A`, making the diagram
```
R[S' √ó P] ‚ü∂ A
  |          |
  v          v
R[S √ó P]  ‚ü∂ B
```
commute.
-/
lemma free_internallyProjective_iff_tensor_condition' (P : LightCondSet.{u}) :
    InternallyProjective ((free R).obj P) ‚Üî
      ‚àÄ {A B : LightCondMod R} (e : A ‚ü∂ B) [Epi e], (‚àÄ (S : LightProfinite)
        (g : (free R).obj (S.toCondensed ‚äó P) ‚ü∂ B), ‚àÉ (S' : LightProfinite)
          (œÄ : S' ‚ü∂ S) (_ : Function.Surjective œÄ) (g' : (free R).obj (S'.toCondensed ‚äó P) ‚ü∂ A),
            ((free R).map (((lightProfiniteToLightCondSet).map œÄ) ‚ñ∑ P)) ‚â´ g = g' ‚â´ e) := by
  rw [internallyProjective_iff_tensor_condition']
  refine ‚ü®fun h A B e he S g ‚Ü¶ ?_, fun h A B e he S g ‚Ü¶ ?_‚ü©
  ¬∑ specialize h e S ((Functor.Monoidal.ŒºIso (free R) _ _).hom ‚â´ g)
    obtain ‚ü®S', œÄ, hœÄ, g', hh‚ü© := h
    refine ‚ü®S', œÄ, hœÄ, (Functor.Monoidal.ŒºIso (free R) _ _).inv ‚â´ g', ?_‚ü©
    rw [Category.assoc, ‚Üê hh]
    -- Generated by `simp?`. Leaving it unsqueezed is too slow
    simp only [Functor.Monoidal.ŒºIso_inv, Functor.comp_obj, Functor.comp_map,
      Functor.Monoidal.ŒºIso_hom, Functor.LaxMonoidal.Œº_natural_left_assoc,
      Functor.Monoidal.Œ¥_Œº_assoc]
  ¬∑ specialize h e S ((Functor.Monoidal.ŒºIso (free R) _ _).inv ‚â´ g)
    obtain ‚ü®S', œÄ, hœÄ, g', hh‚ü© := h
    refine ‚ü®S', œÄ, hœÄ, (Functor.Monoidal.ŒºIso (free R) _ _).hom ‚â´ g', ?_‚ü©
    rw [Category.assoc, ‚Üê hh, ‚Üê Category.assoc, ‚Üê Category.assoc]
    -- Generated by `simp? [‚Üê Functor.LaxMonoidal.Œº_natural_left]`
    -- Leaving it unsqueezed is too slow.
    simp only [Functor.comp_obj, Functor.comp_map, Functor.Monoidal.ŒºIso_hom,
      ‚Üê Functor.LaxMonoidal.Œº_natural_left, Functor.Monoidal.ŒºIso_inv, Category.assoc,
      Functor.Monoidal.Œº_Œ¥, Category.comp_id]

/--
Given a `P : LightProfinite`, the light free light condensed module `R[P]` is internally projective
if and only if, for all `A B : LightCondMod R`, for all epimorphisms `e : A ‚ü∂ B`, for all
`S : LightProfinite` and all morphisms `g : R[P √ó S] ‚ü∂ B`, there exists a `S' : LightProfinite`
with a surjeciton `œÄ : S' ‚ü∂ S` and a morphism `g' : R[P √ó S'] ‚ü∂ A`, making the diagram
```
R[P √ó S'] ‚ü∂ A
  |          |
  v          v
R[P √ó S]  ‚ü∂ B
```
commute.
-/
lemma free_lightProfinite_internallyProjective_iff_tensor_condition (P : LightProfinite.{u}) :
    InternallyProjective ((free R).obj P.toCondensed) ‚Üî
      ‚àÄ {A B : LightCondMod R} (e : A ‚ü∂ B) [Epi e], (‚àÄ (S : LightProfinite)
        (g : (free R).obj ((P ‚äó S).toCondensed) ‚ü∂ B), ‚àÉ (S' : LightProfinite)
          (œÄ : S' ‚ü∂ S) (_ : Function.Surjective œÄ) (g' : (free R).obj (P ‚äó S').toCondensed ‚ü∂ A),
            ((free R).map (lightProfiniteToLightCondSet.map (P ‚óÅ œÄ))) ‚â´ g = g' ‚â´ e) := by
  rw [free_internallyProjective_iff_tensor_condition]
  refine ‚ü®fun h A B e he S g ‚Ü¶ ?_, fun h A B e he S g ‚Ü¶ ?_‚ü©
  ¬∑ specialize h e S ((free R).map (Functor.Monoidal.ŒºIso lightProfiniteToLightCondSet _ _).hom ‚â´ g)
    obtain ‚ü®S', œÄ, hœÄ, g', hh‚ü© := h
    refine ‚ü®S', œÄ, hœÄ, (free R).map (Functor.Monoidal.ŒºIso
        lightProfiniteToLightCondSet _ _).inv ‚â´ g', ?_‚ü©
    rw [Category.assoc, ‚Üê hh]
    simp [-Functor.map_comp, ‚Üê Functor.map_comp_assoc]
  ¬∑ specialize h e S ((free R).map (Functor.Monoidal.ŒºIso lightProfiniteToLightCondSet _ _).inv ‚â´ g)
    obtain ‚ü®S', œÄ, hœÄ, g', hh‚ü© := h
    refine ‚ü®S', œÄ, hœÄ, (free R).map
      (Functor.Monoidal.ŒºIso lightProfiniteToLightCondSet _ _).hom ‚â´ g', ?_‚ü©
    rw [Category.assoc, ‚Üê hh]
    simp [-Functor.map_comp, ‚Üê Functor.map_comp_assoc, ‚Üê Functor.LaxMonoidal.Œº_natural_right_assoc]

/--
Given a `P : LightProfinite`, the light free light condensed module `R[P]` is internally projective
if and only if, for all `A B : LightCondMod R`, for all epimorphisms `e : A ‚ü∂ B`, for all
`S : LightProfinite` and all morphisms `g : R[S √ó P] ‚ü∂ B`, there exists a `S' : LightProfinite`
with a surjeciton `œÄ : S' ‚ü∂ S` and a morphism `g' : R[S' √ó P] ‚ü∂ A`, making the diagram
```
R[S' √ó P] ‚ü∂ A
  |          |
  v          v
R[S √ó P]  ‚ü∂ B
```
commute.
-/
lemma free_lightProfinite_internallyProjective_iff_tensor_condition' (P : LightProfinite.{u}) :
    InternallyProjective ((free R).obj P.toCondensed) ‚Üî
      ‚àÄ {A B : LightCondMod R} (e : A ‚ü∂ B) [Epi e], (‚àÄ (S : LightProfinite)
        (g : (free R).obj ((S ‚äó P).toCondensed) ‚ü∂ B), ‚àÉ (S' : LightProfinite)
          (œÄ : S' ‚ü∂ S) (_ : Function.Surjective œÄ) (g' : (free R).obj (S' ‚äó P).toCondensed ‚ü∂ A),
            ((free R).map (lightProfiniteToLightCondSet.map (œÄ ‚ñ∑ P))) ‚â´ g = g' ‚â´ e) := by
  rw [free_internallyProjective_iff_tensor_condition']
  refine ‚ü®fun h A B e he S g ‚Ü¶ ?_, fun h A B e he S g ‚Ü¶ ?_‚ü©
  ¬∑ specialize h e S ((free R).map (Functor.Monoidal.ŒºIso lightProfiniteToLightCondSet _ _).hom ‚â´ g)
    obtain ‚ü®S', œÄ, hœÄ, g', hh‚ü© := h
    refine ‚ü®S', œÄ, hœÄ, (free R).map (Functor.Monoidal.ŒºIso
        lightProfiniteToLightCondSet _ _).inv ‚â´ g', ?_‚ü©
    rw [Category.assoc, ‚Üê hh]
    simp [-Functor.map_comp, ‚Üê Functor.map_comp_assoc]
  ¬∑ specialize h e S ((free R).map (Functor.Monoidal.ŒºIso lightProfiniteToLightCondSet _ _).inv ‚â´ g)
    obtain ‚ü®S', œÄ, hœÄ, g', hh‚ü© := h
    refine ‚ü®S', œÄ, hœÄ, (free R).map
      (Functor.Monoidal.ŒºIso lightProfiniteToLightCondSet _ _).hom ‚â´ g', ?_‚ü©
    rw [Category.assoc, ‚Üê hh]
    simp [-Functor.map_comp, ‚Üê Functor.map_comp_assoc, ‚Üê Functor.LaxMonoidal.Œº_natural_left_assoc]

end LightCondensed
