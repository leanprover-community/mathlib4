/-
Copyright (c) 2023 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.CategoryTheory.Sites.CoherentEquivalence
import Mathlib.Condensed.Explicit
import Mathlib.Condensed.Light.Abelian
/-!

# The explicit sheaf condition for light condensed sets

We give explicit description of light condensed sets:

* `LightCondensed.ofSheafLightProfinite`: A finite-product-preserving presheaf on `LightProfinite`,
  satisfying `EqualizerCondition`.

The property `EqualizerCondition`Â is defined in `Mathlib/CategoryTheory/Sites/RegularExtensive.lean`
and it says that for any effective epi `X âŸ¶ B` (in this case that is equivalent to being a
continuous surjection), the presheaf `F`Â exhibits `F(B)` as the equalizer of the two maps
`F(X) â‡‰ F(X Ã—_B X)`
-/

universe v u

open CategoryTheory Limits Opposite Functor Presieve regularCoverage

namespace LightProfinite

theorem isSheaf_iff_preservesFiniteProducts_and_equalizerCondition
    (F : LightProfinite.{u}áµ’áµ– â¥¤ Type u) :
    IsSheaf (coherentTopology LightProfinite) F â†”
    Nonempty (PreservesFiniteProducts F) âˆ§ EqualizerCondition F := by
  let e := equivSmallModel LightProfinite.{u}
  rw [â† isSheaf_iff_isSheaf_of_type, e.precoherent_isSheaf_iff (Type u) F]
  haveI : HasFiniteCoproducts (SmallModel.{u, u, u+1} LightProfinite) :=
    âŸ¨fun _ â†¦ Adjunction.hasColimitsOfShape_of_equivalence e.inverseâŸ©
  haveI : HasPullbacks (SmallModel.{u, u, u+1} LightProfinite) :=
    Adjunction.hasLimitsOfShape_of_equivalence e.inverse
  haveI : FinitaryExtensive (SmallModel.{u, u, u+1} LightProfinite) :=
    finitaryExtensive_of_preserves_and_reflects e.inverse
  rw [isSheaf_iff_isSheaf_of_type, isSheaf_coherent_iff_regular_and_extensive]
  apply and_congr
  Â· rw [isSheaf_iff_preservesFiniteProducts]
    have : IsEquivalence e.inverse.op := (inferInstance : IsEquivalence e.op.inverse)
    have : IsEquivalence e.functor.op := (inferInstance : IsEquivalence e.op.functor)
    refine âŸ¨fun âŸ¨hâŸ© â†¦ âŸ¨âŸ¨fun J _ â†¦ ?_âŸ©âŸ©, fun âŸ¨hâŸ© â†¦ âŸ¨âŸ¨fun J _ â†¦ ?_âŸ©âŸ©âŸ©
    Â· have : PreservesLimitsOfShape (Discrete J) ((e.op.functor â‹™ e.op.inverse) â‹™ F) :=
        (inferInstance : PreservesLimitsOfShape _ (e.functor.op â‹™ e.inverse.op â‹™ F))
      exact preservesLimitsOfShapeOfNatIso (isoWhiskerRight e.op.unitIso F).symm
    Â· infer_instance
  Â· rw [EqualizerCondition.isSheaf_iff]
    exact (equalizerCondition_iff_of_equivalence F e).symm

theorem isSheaf_iff_preservesFiniteProducts_and_equalizerCondition'
    {A : Type (u+1)} [Category.{u} A] (G : A â¥¤ Type u)
    [h : HasLimits A] [PreservesLimits G] [ReflectsIsomorphisms G] (F : LightProfinite.{u}áµ’áµ– â¥¤ A) :
    Presheaf.IsSheaf (coherentTopology LightProfinite) F â†”
    Nonempty (PreservesFiniteProducts (F â‹™ G)) âˆ§ EqualizerCondition (F â‹™ G) := by
  let e := equivSmallModel LightProfinite.{u}
  rw [e.precoherent_isSheaf_iff, Presheaf.isSheaf_iff_isSheaf_forget (coherentTopology _)
    (e.inverse.op â‹™ F) G]
  change Presheaf.IsSheaf _ (e.inverse.op â‹™ F â‹™ G) â†” _
  rw [â† e.precoherent_isSheaf_iff, isSheaf_iff_isSheaf_of_type,
    isSheaf_iff_preservesFiniteProducts_and_equalizerCondition]

end LightProfinite

namespace LightCondensed

variable {A : Type (u+1)} [Category.{u} A] (G : A â¥¤ Type u) [HasLimits A] [PreservesLimits G]
    [ReflectsIsomorphisms G]

/--
The condensed set associated to a presheaf on `Profinite` which preserves finite products and
satisfies the equalizer condition.
-/
noncomputable def ofSheafLightProfinite (F : LightProfinite.{u}áµ’áµ– â¥¤ A) [PreservesFiniteProducts F]
    (hF : EqualizerCondition (F â‹™ G)) : LightCondensed A where
    val := F
    cond := by
      rw [LightProfinite.isSheaf_iff_preservesFiniteProducts_and_equalizerCondition' G F]
      exact âŸ¨âŸ¨âŸ¨fun _ _ â†¦ inferInstanceâŸ©âŸ©, hFâŸ©

end LightCondensed

namespace LightCondSet

/-- A `LightCondSet` version of `LightCondensed.ofSheafLightProfinite`.Â -/
noncomputable abbrev ofSheafLightProfinite (F : LightProfinite.{u}áµ’áµ– â¥¤ Type u)
    [PreservesFiniteProducts F] (hF : EqualizerCondition F) : LightCondSet :=
  LightCondensed.ofSheafLightProfinite (ðŸ­ _) F hF

/-- A light condensed set satisfies the equalizer condition. -/
theorem equalizerCondition (X : LightCondSet) : EqualizerCondition X.val :=
  LightProfinite.isSheaf_iff_preservesFiniteProducts_and_equalizerCondition'
    (ðŸ­ _) X.val |>.mp X.cond |>.2

/-- A light condensed set preserves finite products. -/
noncomputable instance (X : LightCondSet) : PreservesFiniteProducts X.val :=
  LightProfinite.isSheaf_iff_preservesFiniteProducts_and_equalizerCondition'
    (ðŸ­ _) X.val |>.mp X.cond |>.1.some

end LightCondSet

namespace LightCondAb

/-- A `LightCondAb` version of `LightCondensed.ofSheafLightProfinite`.Â -/
noncomputable abbrev ofSheafProfinite (F : LightProfinite.{u}áµ’áµ– â¥¤ AddCommGroupCat.{u})
    [PreservesFiniteProducts F] (hF : EqualizerCondition (F â‹™ forget _)) : LightCondAb :=
  LightCondensed.ofSheafLightProfinite (forget _) F hF

end LightCondAb
