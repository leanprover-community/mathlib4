/-
Copyright (c) 2024 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.Condensed.Light.TopComparison
/-!

# The adjunction between light condensed sets and topological spaces

This file defines the functor `lightCondSetToTopCat : LightCondSet.{u} ‚•§ TopCat.{u}` which is
left adjoint to `topCatToLightCondSet : TopCat.{u} ‚•§ LightCondSet.{u}`. We prove that the counit
is bijective (but not in general an isomorphism) and conclude that the right adjoint is faithful.
-/

universe u

open LightCondensed LightCondSet CategoryTheory

attribute [local instance] ConcreteCategory.instFunLike

namespace LightCondSet

variable (X : LightCondSet.{u})

/-- Auxiliary definition to define the topology on `X(*)` for a light condensed set `X`. -/
private def _root_.LightProfinite.const (S : LightProfinite.{u}) (s : S) :
    LightProfinite.of PUnit.{u+1} ‚ü∂ S :=
  ContinuousMap.const _ s

/-- Auxiliary definition to define the topology on `X(*)` for a light condensed set `X`. -/
private def coinducingCoprod :
    (Œ£ (i : (S : LightProfinite.{u}) √ó X.val.obj ‚ü®S‚ü©), i.fst) ‚Üí
      X.val.obj ‚ü®LightProfinite.of PUnit‚ü© :=
  fun ‚ü®‚ü®S, i‚ü©, s‚ü© ‚Ü¶ X.val.map (S.const s).op i

/-- Let `X`¬†be a light condensed set. We define a topology on `X(*)` as the quotient topology of
all the maps from light profinite sets `S` to `X(*)`, corresponding to elements of `X(S)`.
In other words, the topology coinduced by the map `LightCondSet.coinducingCoprod` above. -/
local instance underlyingTopologicalSpace :
    TopologicalSpace (X.val.obj ‚ü®LightProfinite.of PUnit‚ü©) :=
  TopologicalSpace.coinduced (coinducingCoprod X) inferInstance

/-- The object part of the functor `LightCondSet ‚•§ TopCat`¬† -/
def toTopCat : TopCat.{u} := TopCat.of (X.val.obj ‚ü®LightProfinite.of PUnit‚ü©)

variable {X} {Y : LightCondSet} (f : X ‚ü∂ Y)

/-- The map part of the functor `LightCondSet ‚•§ TopCat`¬† -/
@[simps]
def toTopCatMap : X.toTopCat ‚ü∂ Y.toTopCat where
  toFun := f.val.app ‚ü®LightProfinite.of PUnit‚ü©
  continuous_toFun := by
    rw [continuous_coinduced_dom]
    apply continuous_sigma
    intro ‚ü®S, x‚ü©
    simp only [Function.comp_apply, coinducingCoprod]
    have : (fun (a : S) ‚Ü¶ f.val.app ‚ü®LightProfinite.of PUnit‚ü© (X.val.map (S.const a).op x)) =
        (fun (a : S) ‚Ü¶ Y.val.map (S.const a).op (f.val.app ‚ü®S‚ü© x)) :=
      funext fun a ‚Ü¶ NatTrans.naturality_apply f.val (S.const a).op x
    rw [this]
    suffices ‚àÄ (i : (T : LightProfinite.{u}) √ó Y.val.obj ‚ü®T‚ü©),
        Continuous (fun (a : i.fst) ‚Ü¶ Y.coinducingCoprod ‚ü®i, a‚ü©) from this ‚ü®_, _‚ü©
    rw [‚Üê continuous_sigma_iff]
    apply continuous_coinduced_rng

/-- The functor `LightCondSet ‚•§ TopCat`¬† -/
@[simps]
def _root_.lightCondSetToTopCat : LightCondSet.{u} ‚•§ TopCat.{u} where
  obj X := X.toTopCat
  map f := toTopCatMap f

/-- The counit of the adjunction `lightCondSetToTopCat ‚ä£ topCatToLightCondSet` -/
def topCatAdjunctionCounit (X : TopCat.{u}) : X.toLightCondSet.toTopCat ‚ü∂ X where
  toFun x := x.1 PUnit.unit
  continuous_toFun := by
    rw [continuous_coinduced_dom]
    continuity

/-- The counit of the adjunction `lightCondSetToTopCat ‚ä£ topCatToLightCondSet` is always bijective,
but not an isomorphism in general (the inverse isn't continuous unless `X`¬†is sequential).
-/
def topCatAdjunctionCounitEquiv (X : TopCat.{u}) : X.toLightCondSet.toTopCat ‚âÉ X where
  toFun := topCatAdjunctionCounit X
  invFun x := ContinuousMap.const _ x
  left_inv _ := rfl
  right_inv _ := rfl

lemma topCatAdjunctionCounit_bijective (X : TopCat.{u}) :
    Function.Bijective (topCatAdjunctionCounit X) :=
  (topCatAdjunctionCounitEquiv X).bijective

/-- The unit of the adjunction `lightCondSetToTopCat ‚ä£ topCatToLightCondSet` -/
@[simps val_app val_app_apply]
def topCatAdjunctionUnit (X : LightCondSet.{u}) : X ‚ü∂ X.toTopCat.toLightCondSet where
  val := {
    app := fun S x ‚Ü¶ {
      toFun := fun s ‚Ü¶ X.val.map (S.unop.const s).op x
      continuous_toFun := by
        suffices ‚àÄ (i : (T : LightProfinite.{u}) √ó X.val.obj ‚ü®T‚ü©),
          Continuous (fun (a : i.fst) ‚Ü¶ X.coinducingCoprod ‚ü®i, a‚ü©) from this ‚ü®_, _‚ü©
        rw [‚Üê continuous_sigma_iff]
        apply continuous_coinduced_rng }
    naturality := fun _ _ _ ‚Ü¶ by
      ext
      simp only [types_comp_apply, ContinuousMap.coe_mk, TopCat.toCondensedSet_val_map,
        ContinuousMap.comp_apply, ‚Üê FunctorToTypes.map_comp_apply]
      rfl }

/-- The adjunction `lightCondSetToTopCat ‚ä£ topCatToLightCondSet` -/
noncomputable def topCatAdjunction : lightCondSetToTopCat.{u} ‚ä£ topCatToLightCondSet :=
  Adjunction.mkOfUnitCounit {
    unit := { app := topCatAdjunctionUnit }
    counit := { app := topCatAdjunctionCounit }
    left_triangle := by
      ext Y
      change Y.val.map (ùüô _) _ = _
      simp }

instance (X : TopCat) : Epi (topCatAdjunction.counit.app X) := by
  rw [TopCat.epi_iff_surjective]
  exact (topCatAdjunctionCounit_bijective _).2

instance : topCatToLightCondSet.Faithful := topCatAdjunction.faithful_R_of_epi_counit_app

end LightCondSet
