/-
Copyright (c) 2024 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.Condensed.TopComparison
import Mathlib.Topology.Category.CompactlyGenerated
/-!

# The adjunction between condensed sets and topological spaces

This file defines the functor `condensedSetToTopCat : CondensedSet.{u} ‚•§ TopCat.{u + 1}` which is
left adjoint to `topCatToCondensedSet : TopCat.{u + 1} ‚•§ CondensedSet.{u}`. We prove that the counit
is bijective (but not in general an isomorphism) and conclude that the right adjoint is faithful.

The counit is an isomorphism for compactly generated spaces, and we conclude that the functor
`topCatToCondensedSet` is fully faithful when restricted to compactly generated spaces.
-/

universe u

open Condensed CondensedSet CategoryTheory CompHaus

variable (X : CondensedSet.{u})

/-- Auxiliary definition to define the topology on `X(*)` for a condensed set `X`. -/
private def CondensedSet.coinducingCoprod :
    (Œ£ (i : (S : CompHaus.{u}) √ó X.val.obj ‚ü®S‚ü©), i.fst) ‚Üí X.val.obj ‚ü®of PUnit‚ü© :=
  fun ‚ü®‚ü®_, i‚ü©, s‚ü© ‚Ü¶ X.val.map ((of PUnit.{u + 1}).const s).op i

/-- Let `X` be a condensed set. We define a topology on `X(*)` as the quotient topology of
all the maps from compact Hausdorff `S` spaces to `X(*)`, corresponding to elements of `X(S)`.
In other words, the topology coinduced by the map `CondensedSet.coinducingCoprod` above. -/
local instance : TopologicalSpace (X.val.obj ‚ü®CompHaus.of PUnit‚ü©) :=
  TopologicalSpace.coinduced (coinducingCoprod X) inferInstance

/-- The object part of the functor `CondensedSet ‚•§ TopCat` -/
abbrev CondensedSet.toTopCat : TopCat.{u + 1} := TopCat.of (X.val.obj ‚ü®of PUnit‚ü©)

namespace CondensedSet

lemma continuous_coinducingCoprod {S : CompHaus.{u}} (x : X.val.obj ‚ü®S‚ü©) :
    Continuous fun a ‚Ü¶ (X.coinducingCoprod ‚ü®‚ü®S, x‚ü©, a‚ü©) := by
  suffices ‚àÄ (i : (T : CompHaus.{u}) √ó X.val.obj ‚ü®T‚ü©),
      Continuous (fun (a : i.fst) ‚Ü¶ X.coinducingCoprod ‚ü®i, a‚ü©) from this ‚ü®_, _‚ü©
  rw [‚Üê continuous_sigma_iff]
  apply continuous_coinduced_rng

variable {X} {Y : CondensedSet} (f : X ‚ü∂ Y)

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
/-- The map part of the functor `CondensedSet ‚•§ TopCat` -/
@[simps!]
def toTopCatMap : X.toTopCat ‚ü∂ Y.toTopCat :=
  TopCat.ofHom
  { toFun := f.val.app ‚ü®of PUnit‚ü©
    continuous_toFun := by
      rw [continuous_coinduced_dom]
      apply continuous_sigma
      intro ‚ü®S, x‚ü©
      simp only [Function.comp_apply, coinducingCoprod]
      rw [show (fun (a : S) ‚Ü¶
          f.val.app ‚ü®of PUnit‚ü© (X.val.map ((of PUnit.{u + 1}).const a).op x)) = _
        from funext fun a ‚Ü¶ NatTrans.naturality_apply f.val ((of PUnit.{u + 1}).const a).op x]
      exact continuous_coinducingCoprod Y _ }

end CondensedSet

/-- The functor `CondensedSet ‚•§ TopCat` -/
@[simps]
def condensedSetToTopCat : CondensedSet.{u} ‚•§ TopCat.{u + 1} where
  obj X := X.toTopCat
  map f := toTopCatMap f

namespace CondensedSet

/-- The counit of the adjunction `condensedSetToTopCat ‚ä£ topCatToCondensedSet` -/
noncomputable def topCatAdjunctionCounit (X : TopCat.{u + 1}) : X.toCondensedSet.toTopCat ‚ü∂ X :=
  TopCat.ofHom
  { toFun x := x.1 PUnit.unit
    continuous_toFun := by
      rw [continuous_coinduced_dom]
      continuity }

/-- `simp`-normal form of the lemma that `@[simps]` would generate. -/
@[simp] lemma topCatAdjunctionCounit_hom_apply (X : TopCat) (x) :
    -- We have to specify here to not infer the `TopologicalSpace` instance on `C(PUnit, X)`,
    -- which suggests type synonyms are being unfolded too far somewhere.
    DFunLike.coe (F := @ContinuousMap C(PUnit, X) X (_) _)
        (TopCat.Hom.hom (topCatAdjunctionCounit X)) x =
      x PUnit.unit := rfl

/-- The counit of the adjunction `condensedSetToTopCat ‚ä£ topCatToCondensedSet` is always bijective,
but not an isomorphism in general (the inverse isn't continuous unless `X` is compactly generated).
-/
noncomputable def topCatAdjunctionCounitEquiv (X : TopCat.{u + 1}) :
    X.toCondensedSet.toTopCat ‚âÉ X where
  toFun := topCatAdjunctionCounit X
  invFun x := ContinuousMap.const _ x

lemma topCatAdjunctionCounit_bijective (X : TopCat.{u + 1}) :
    Function.Bijective (topCatAdjunctionCounit X) :=
  (topCatAdjunctionCounitEquiv X).bijective

/-- The unit of the adjunction `condensedSetToTopCat ‚ä£ topCatToCondensedSet` -/
@[simps val_app val_app_apply]
noncomputable def topCatAdjunctionUnit (X : CondensedSet.{u}) : X ‚ü∂ X.toTopCat.toCondensedSet where
  val := {
    app := fun S x ‚Ü¶ {
      toFun := fun s ‚Ü¶ X.val.map ((of PUnit.{u + 1}).const s).op x
      continuous_toFun := by
        suffices ‚àÄ (i : (T : CompHaus.{u}) √ó X.val.obj ‚ü®T‚ü©),
          Continuous (fun (a : i.fst) ‚Ü¶ X.coinducingCoprod ‚ü®i, a‚ü©) from this ‚ü®_, _‚ü©
        rw [‚Üê continuous_sigma_iff]
        apply continuous_coinduced_rng }
    naturality := fun _ _ _ ‚Ü¶ by
      ext
      simp only [TopCat.toSheafCompHausLike_val_obj,
        Opposite.op_unop, types_comp_apply, TopCat.toSheafCompHausLike_val_map,
        ‚Üê FunctorToTypes.map_comp_apply]
      rfl }

/-- The adjunction `condensedSetToTopCat ‚ä£ topCatToCondensedSet` -/
noncomputable def topCatAdjunction : condensedSetToTopCat.{u} ‚ä£ topCatToCondensedSet where
  unit.app := topCatAdjunctionUnit
  counit.app := topCatAdjunctionCounit
  left_triangle_components Y := by
    ext
    change Y.val.map (ùüô _) _ = _
    simp

instance (X : TopCat) : Epi (topCatAdjunction.counit.app X) := by
  rw [TopCat.epi_iff_surjective]
  exact (topCatAdjunctionCounit_bijective _).2

instance : topCatToCondensedSet.Faithful := topCatAdjunction.faithful_R_of_epi_counit_app

open CompactlyGenerated

instance (X : CondensedSet.{u}) : UCompactlyGeneratedSpace.{u, u + 1} X.toTopCat := by
  apply uCompactlyGeneratedSpace_of_continuous_maps
  intro Y _ f h
  rw [continuous_coinduced_dom, continuous_sigma_iff]
  exact fun ‚ü®S, s‚ü© ‚Ü¶ h S ‚ü®_, continuous_coinducingCoprod X _‚ü©

instance (X : CondensedSet.{u}) :
    UCompactlyGeneratedSpace.{u, u + 1} (condensedSetToTopCat.obj X) :=
  inferInstanceAs (UCompactlyGeneratedSpace.{u, u + 1} X.toTopCat)

/-- The functor from condensed sets to topological spaces lands in compactly generated spaces. -/
def condensedSetToCompactlyGenerated : CondensedSet.{u} ‚•§ CompactlyGenerated.{u, u + 1} where
  obj X := CompactlyGenerated.of (condensedSetToTopCat.obj X)
  map f := toTopCatMap f

/--
The functor from topological spaces to condensed sets restricted to compactly generated spaces.
-/
noncomputable def compactlyGeneratedToCondensedSet :
    CompactlyGenerated.{u, u + 1} ‚•§ CondensedSet.{u} :=
  compactlyGeneratedToTop ‚ãô topCatToCondensedSet


/--
The adjunction `condensedSetToTopCat ‚ä£ topCatToCondensedSet` restricted to compactly generated
spaces.
-/
noncomputable def compactlyGeneratedAdjunction :
    condensedSetToCompactlyGenerated ‚ä£ compactlyGeneratedToCondensedSet :=
  topCatAdjunction.restrictFullyFaithful (iC := ùü≠ _) (iD := compactlyGeneratedToTop)
    (Functor.FullyFaithful.id _) fullyFaithfulCompactlyGeneratedToTop
    (Iso.refl _) (Iso.refl _)

/--
The counit of the adjunction `condensedSetToCompactlyGenerated ‚ä£ compactlyGeneratedToCondensedSet`
is a homeomorphism.
-/
noncomputable def compactlyGeneratedAdjunctionCounitHomeo
    (X : TopCat.{u + 1}) [UCompactlyGeneratedSpace.{u} X] :
    X.toCondensedSet.toTopCat ‚âÉ‚Çú X where
  toEquiv := topCatAdjunctionCounitEquiv X
  continuous_toFun := (topCatAdjunctionCounit X).hom.continuous
  continuous_invFun := by
    apply continuous_from_uCompactlyGeneratedSpace
    exact fun _ _ ‚Ü¶ continuous_coinducingCoprod X.toCondensedSet _

/--
The counit of the adjunction `condensedSetToCompactlyGenerated ‚ä£ compactlyGeneratedToCondensedSet`
is an isomorphism.
-/
noncomputable def compactlyGeneratedAdjunctionCounitIso (X : CompactlyGenerated.{u, u + 1}) :
    condensedSetToCompactlyGenerated.obj (compactlyGeneratedToCondensedSet.obj X) ‚âÖ X :=
  isoOfHomeo (compactlyGeneratedAdjunctionCounitHomeo X.toTop)

instance : IsIso compactlyGeneratedAdjunction.counit := by
  rw [NatTrans.isIso_iff_isIso_app]
  intro X
  exact inferInstanceAs (IsIso (compactlyGeneratedAdjunctionCounitIso X).hom)

/--
The functor from topological spaces to condensed sets restricted to compactly generated spaces
is fully faithful.
-/
noncomputable def fullyFaithfulCompactlyGeneratedToCondensedSet :
    compactlyGeneratedToCondensedSet.FullyFaithful :=
  compactlyGeneratedAdjunction.fullyFaithfulROfIsIsoCounit

end CondensedSet
