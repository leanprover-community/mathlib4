/-
Copyright (c) 2023 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson, Riccardo Brasca, Filippo A. E. Nuccio
-/
import Mathlib.Condensed.Abelian
import Mathlib.Condensed.Equivalence
import Mathlib.Topology.Category.Profinite.EffectiveEpi
import Mathlib.Topology.Category.Stonean.EffectiveEpi
/-!

# The explicit sheaf condition for condensed sets

We give the following three explicit descriptions of condensed sets:

* `Condensed.ofSheafStonean`: A finite-product-preserving presheaf on `Stonean`.

* `Condensed.ofSheafProfinite`: A finite-product-preserving presheaf on `Profinite`, satisfying
  `EqualizerCondition`.

* `Condensed.ofSheafStonean`: A finite-product-preserving presheaf on `CompHaus`, satisfying
  `EqualizerCondition`.

The property `EqualizerCondition`Â is defined in `Mathlib/CategoryTheory/Sites/RegularExtensive.lean`
and it says that for any effective epi `X âŸ¶ B` (in this case that is equivalent to being a
continuous surjection), the presheaf `F`Â exhibits `F(B)` as the equalizer of the two maps
`F(X) â‡‰ F(X Ã—_B X)`
-/

universe v u

open CategoryTheory Limits Opposite Functor Presieve regularCoverage

namespace CategoryTheory

variable {C : Type u} [Category.{v} C] (F : Cáµ’áµ– â¥¤ Type (max u v)) [Preregular C]
  [FinitaryPreExtensive C]

theorem isSheaf_coherent_iff_regular_and_extensive : IsSheaf (coherentTopology C) F â†”
    IsSheaf (extensiveCoverage C).toGrothendieck F âˆ§
    IsSheaf (regularCoverage C).toGrothendieck F := by
  rw [â† extensive_regular_generate_coherent]
  exact isSheaf_sup (extensiveCoverage C) (regularCoverage C) F

end CategoryTheory

namespace CompHaus

theorem isSheaf_iff_preservesFiniteProducts_and_equalizerCondition
    (F : CompHaus.{u}áµ’áµ– â¥¤ Type (u+1)) :
    IsSheaf (coherentTopology CompHaus) F â†”
    Nonempty (PreservesFiniteProducts F) âˆ§ EqualizerCondition F := by
  rw [isSheaf_coherent_iff_regular_and_extensive]
  apply and_congr
  Â· exact isSheaf_iff_preservesFiniteProducts F
  Â· exact EqualizerCondition.isSheaf_iff F

theorem isSheaf_iff_preservesFiniteProducts_and_equalizerCondition'
    {A : Type (u+2)} [Category.{u+1} A] (G : A â¥¤ Type (u+1))
    [HasLimits A] [PreservesLimits G] [ReflectsIsomorphisms G] (F : CompHaus.{u}áµ’áµ– â¥¤ A) :
    Presheaf.IsSheaf (coherentTopology CompHaus) F â†”
    Nonempty (PreservesFiniteProducts (F â‹™ G)) âˆ§ EqualizerCondition (F â‹™ G) := by
  rw [Presheaf.isSheaf_iff_isSheaf_forget (coherentTopology CompHaus) F G,
    isSheaf_iff_isSheaf_of_type, isSheaf_iff_preservesFiniteProducts_and_equalizerCondition]

noncomputable
instance {A B : Type*} [Category A] [Category B] (F : B â¥¤ A) (E : A)  [PreservesFiniteProducts F] :
    PreservesFiniteProducts (F â‹™ coyoneda.obj (op E)) :=
  âŸ¨fun J _ â†¦ @compPreservesLimitsOfShape _ _ _ _ _ _ _ _ F (coyoneda.obj (op E))
    (PreservesFiniteProducts.preserves J) ((preservesLimitsOfSizeShrink _).preservesLimitsOfShape)âŸ©

end CompHaus

namespace Profinite

theorem isSheaf_iff_preservesFiniteProducts_and_equalizerCondition
    (F : Profinite.{u}áµ’áµ– â¥¤ Type (u+1)) :
    IsSheaf (coherentTopology Profinite) F â†”
    Nonempty (PreservesFiniteProducts F) âˆ§ EqualizerCondition F := by
  rw [isSheaf_coherent_iff_regular_and_extensive]
  apply and_congr
  Â· exact isSheaf_iff_preservesFiniteProducts F
  Â· exact EqualizerCondition.isSheaf_iff F

theorem isSheaf_iff_preservesFiniteProducts_and_equalizerCondition'
    {A : Type (u+2)} [Category.{u+1} A] (G : A â¥¤ Type (u+1))
    [HasLimits A] [PreservesLimits G] [ReflectsIsomorphisms G] (F : Profinite.{u}áµ’áµ– â¥¤ A) :
    Presheaf.IsSheaf (coherentTopology Profinite) F â†”
    Nonempty (PreservesFiniteProducts (F â‹™ G)) âˆ§ EqualizerCondition (F â‹™ G) := by
  rw [Presheaf.isSheaf_iff_isSheaf_forget (coherentTopology Profinite) F G,
    isSheaf_iff_isSheaf_of_type, isSheaf_iff_preservesFiniteProducts_and_equalizerCondition]

end Profinite

namespace Stonean

theorem isSheaf_iff_preservesFiniteProducts
    (F : Stonean.{u}áµ’áµ– â¥¤ Type (u+1)) :
    IsSheaf (coherentTopology Stonean) F â†” Nonempty (PreservesFiniteProducts F) := by
  rw [isSheaf_coherent_iff_regular_and_extensive, and_iff_left ?_]
  Â· exact CategoryTheory.isSheaf_iff_preservesFiniteProducts F
  Â· rw [Presieve.isSheaf_coverage]
    intro X R âŸ¨Y, hRâŸ©
    have _ : R.regular := âŸ¨Y, hRâŸ©
    exact isSheafFor_regular_of_projective R F

theorem isSheaf_iff_preservesFiniteProducts'
    {A : Type (u+2)} [Category.{u+1} A] (G : A â¥¤ Type (u+1))
    [HasLimits A] [PreservesLimits G] [ReflectsIsomorphisms G] (F : Stonean.{u}áµ’áµ– â¥¤ A) :
    Presheaf.IsSheaf (coherentTopology Stonean) F â†”
    Nonempty (PreservesFiniteProducts (F â‹™ G)) := by
  rw [Presheaf.isSheaf_iff_isSheaf_forget (coherentTopology Stonean) F G,
    isSheaf_iff_isSheaf_of_type, isSheaf_iff_preservesFiniteProducts]

end Stonean

namespace Condensed

variable {A : Type (u+2)} [Category.{u+1} A] (G : A â¥¤ Type (u+1)) [HasLimits A] [PreservesLimits G]
    [ReflectsIsomorphisms G]

/-- The condensed set associated to a finite-product-preserving presheaf on `Stonean`. -/
noncomputable def ofSheafStonean (F : Stonean.{u}áµ’áµ– â¥¤ A) [PreservesFiniteProducts F] :
    Condensed A :=
  StoneanCompHaus.equivalence A |>.functor.obj {
    val := F
    cond := by
      rw [Stonean.isSheaf_iff_preservesFiniteProducts' G F]
      exact âŸ¨âŸ¨fun _ _ â†¦ inferInstanceâŸ©âŸ© }

/--
The condensed set associated to a presheaf on `Profinite` which preserves finite products and
satisfies the equalizer condition.
-/
noncomputable def ofSheafProfinite (F : Profinite.{u}áµ’áµ– â¥¤ A) [PreservesFiniteProducts F]
    (hF : EqualizerCondition (F â‹™ G)) : Condensed A :=
  ProfiniteCompHaus.equivalence A |>.functor.obj {
    val := F
    cond := by
      rw [Profinite.isSheaf_iff_preservesFiniteProducts_and_equalizerCondition' G F]
      exact âŸ¨âŸ¨âŸ¨fun _ _ â†¦ inferInstanceâŸ©âŸ©, hFâŸ© }

/--
The condensed set associated to a presheaf on `CompHaus` which preserves finite products and
satisfies the equalizer condition.
-/
noncomputable def ofSheafCompHaus (F : CompHaus.{u}áµ’áµ– â¥¤ A) [PreservesFiniteProducts F]
    (hF : EqualizerCondition (F â‹™ G)) : Condensed A where
  val := F
  cond := by
    rw [CompHaus.isSheaf_iff_preservesFiniteProducts_and_equalizerCondition' G F]
    exact âŸ¨âŸ¨âŸ¨fun _ _ â†¦ inferInstanceâŸ©âŸ©, hFâŸ©

end Condensed

namespace CondensedSet

/-- A `CondensedSet` version of `Condensed.ofSheafStonean`.Â -/
noncomputable abbrev ofSheafStonean (F : Stonean.{u}áµ’áµ– â¥¤ Type (u+1)) [PreservesFiniteProducts F] :
    CondensedSet :=
  Condensed.ofSheafStonean (ðŸ­ _) F

/-- A `CondensedSet` version of `Condensed.ofSheafProfinite`.Â -/
noncomputable abbrev ofSheafProfinite (F : Profinite.{u}áµ’áµ– â¥¤ Type (u+1))
    [PreservesFiniteProducts F] (hF : EqualizerCondition F) : CondensedSet :=
  Condensed.ofSheafProfinite (ðŸ­ _) F hF

/-- A `CondensedSet` version of `Condensed.ofSheafCompHaus`.Â -/
noncomputable abbrev ofSheafCompHaus (F : CompHaus.{u}áµ’áµ– â¥¤ Type (u+1))
    [PreservesFiniteProducts F] (hF : EqualizerCondition F) : CondensedSet :=
  Condensed.ofSheafCompHaus (ðŸ­ _) F hF

/-- A condensed set satisfies the equalizer condition. -/
theorem equalizerCondition (X : CondensedSet) : EqualizerCondition X.val :=
  CompHaus.isSheaf_iff_preservesFiniteProducts_and_equalizerCondition' (ðŸ­ _) X.val |>.mp X.cond |>.2

/-- A condensed set preserves finite products. -/
noncomputable instance (X : CondensedSet) : PreservesFiniteProducts X.val :=
  CompHaus.isSheaf_iff_preservesFiniteProducts_and_equalizerCondition' (ðŸ­ _) X.val |>.mp
    X.cond |>.1.some

end CondensedSet

namespace CondensedAb

/-- A `CondensedAb` version of `Condensed.ofSheafStonean`.Â -/
noncomputable abbrev ofSheafStonean (F : Stonean.{u}áµ’áµ– â¥¤ AddCommGroupCat.{u+1})
    [PreservesFiniteProducts F] : CondensedAb :=
  Condensed.ofSheafStonean (forget _) F

/-- A `CondensedAb` version of `Condensed.ofSheafProfinite`.Â -/
noncomputable abbrev ofSheafProfinite (F : Profinite.{u}áµ’áµ– â¥¤ AddCommGroupCat.{u+1})
    [PreservesFiniteProducts F] (hF : EqualizerCondition (F â‹™ forget _)) : CondensedAb :=
  Condensed.ofSheafProfinite (forget _) F hF

/-- A `CondensedAb` version of `Condensed.ofSheafCompHaus`.Â -/
noncomputable abbrev ofSheafCompHaus (F : CompHaus.{u}áµ’áµ– â¥¤ AddCommGroupCat.{u+1})
    [PreservesFiniteProducts F] (hF : EqualizerCondition (F â‹™ forget _)) : CondensedAb :=
  Condensed.ofSheafCompHaus (forget _) F hF

end CondensedAb
