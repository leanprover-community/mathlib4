/-
Copyright (c) 2023 Michael Stoll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Stoll
-/
import Mathlib.NumberTheory.EulerProduct.ExpLog
import Mathlib.NumberTheory.LSeries.Dirichlet

/-!
# The Euler Product for the Riemann Zeta Function and Dirichlet L-Series

The first main result of this file is the Euler Product formula for the Riemann Î¶ function
$$\prod_p \frac{1}{1 - p^{-s}}
   = \lim_{n \to \infty} \prod_{p < n} \frac{1}{1 - p^{-s}} = \zeta(s)$$
for $s$ with real part $> 1$ ($p$ runs through the primes).
`riemannZeta_eulerProduct` is the second equality above. There are versions
`riemannZeta_eulerProduct_hasProd` and `riemannZeta_eulerProduct_tprod` in terms of `HasProd`
and `tprod`, respectively.

The second result is `dirichletLSeries_eulerProduct` (with variants
`dirichletLSeries_eulerProduct_hasProd` and `dirichletLSeries_eulerProduct_tprod`),
which is the analogous statement for Dirichlet L-series.
-/

open Complex

variable {s : â„‚}

/-- When `s â‰  0`, the map `n â†¦ n^(-s)` is completely multiplicative and vanishes at zero. -/
noncomputable
def riemannZetaSummandHom (hs : s â‰  0) : â„• â†’*â‚€ â„‚ where
  toFun n := (n : â„‚) ^ (-s)
  map_zero' := by simp [hs]
  map_one' := by simp
  map_mul' m n := by
    simpa only [Nat.cast_mul, ofReal_natCast]
      using mul_cpow_ofReal_nonneg m.cast_nonneg n.cast_nonneg _

/-- When `Ï‡` is a Dirichlet character and `s â‰  0`, the map `n â†¦ Ï‡ n * n^(-s)` is completely
multiplicative and vanishes at zero. -/
noncomputable
def dirichletSummandHom {n : â„•} (Ï‡ : DirichletCharacter â„‚ n) (hs : s â‰  0) : â„• â†’*â‚€ â„‚ where
  toFun n := Ï‡ n * (n : â„‚) ^ (-s)
  map_zero' := by simp [hs]
  map_one' := by simp
  map_mul' m n := by
    simp_rw [â† ofReal_natCast]
    simpa only [Nat.cast_mul, IsUnit.mul_iff, not_and, map_mul, ofReal_mul,
      mul_cpow_ofReal_nonneg m.cast_nonneg n.cast_nonneg _]
      using mul_mul_mul_comm ..

/-- When `s.re > 1`, the map `n â†¦ n^(-s)` is norm-summable. -/
lemma summable_riemannZetaSummand (hs : 1 < s.re) :
    Summable (fun n â†¦ â€–riemannZetaSummandHom (ne_zero_of_one_lt_re hs) nâ€–) := by
  simp only [riemannZetaSummandHom, MonoidWithZeroHom.coe_mk, ZeroHom.coe_mk]
  convert Real.summable_nat_rpow_inv.mpr hs with n
  rw [â† ofReal_natCast,
    norm_cpow_eq_rpow_re_of_nonneg (Nat.cast_nonneg n) <| re_neg_ne_zero_of_one_lt_re hs,
    neg_re, Real.rpow_neg <| Nat.cast_nonneg n]

lemma tsum_riemannZetaSummand (hs : 1 < s.re) :
    âˆ‘' (n : â„•), riemannZetaSummandHom (ne_zero_of_one_lt_re hs) n = riemannZeta s := by
  have hsum := summable_riemannZetaSummand hs
  rw [zeta_eq_tsum_one_div_nat_add_one_cpow hs, hsum.of_norm.tsum_eq_zero_add, map_zero, zero_add]
  simp only [riemannZetaSummandHom, cpow_neg, MonoidWithZeroHom.coe_mk, ZeroHom.coe_mk,
    Nat.cast_add, Nat.cast_one, one_div]

/-- When `s.re > 1`, the map `n â†¦ Ï‡(n) * n^(-s)` is norm-summable. -/
lemma summable_dirichletSummand {N : â„•} (Ï‡ : DirichletCharacter â„‚ N) (hs : 1 < s.re) :
    Summable (fun n â†¦ â€–dirichletSummandHom Ï‡ (ne_zero_of_one_lt_re hs) nâ€–) := by
  simp only [dirichletSummandHom, MonoidWithZeroHom.coe_mk, ZeroHom.coe_mk, norm_mul]
  exact (summable_riemannZetaSummand hs).of_nonneg_of_le (fun _ â†¦ by positivity)
    (fun n â†¦ mul_le_of_le_one_left (norm_nonneg _) <| Ï‡.norm_le_one n)

open scoped LSeries.notation in
lemma tsum_dirichletSummand {N : â„•} (Ï‡ : DirichletCharacter â„‚ N) (hs : 1 < s.re) :
    âˆ‘' (n : â„•), dirichletSummandHom Ï‡ (ne_zero_of_one_lt_re hs) n = L â†—Ï‡ s := by
  simp only [dirichletSummandHom, cpow_neg, MonoidWithZeroHom.coe_mk, ZeroHom.coe_mk, LSeries,
    LSeries.term_of_ne_zero' (ne_zero_of_one_lt_re hs), div_eq_mul_inv]

open Filter Nat Topology EulerProduct

/-- The Euler product for the Riemann Î¶ function, valid for `s.re > 1`.
This version is stated in terms of `HasProd`. -/
theorem riemannZeta_eulerProduct_hasProd (hs : 1 < s.re) :
    HasProd (fun p : Primes â†¦ (1 - (p : â„‚) ^ (-s))â»Â¹) (riemannZeta s) := by
  rw [â† tsum_riemannZetaSummand hs]
  apply eulerProduct_completely_multiplicative_hasProd <| summable_riemannZetaSummand hs

/-- The Euler product for the Riemann Î¶ function, valid for `s.re > 1`.
This version is stated in terms of `tprod`. -/
theorem riemannZeta_eulerProduct_tprod (hs : 1 < s.re) :
    âˆ' p : Primes, (1 - (p : â„‚) ^ (-s))â»Â¹ = riemannZeta s :=
  (riemannZeta_eulerProduct_hasProd hs).tprod_eq

/-- The Euler product for the Riemann Î¶ function, valid for `s.re > 1`.
This version is stated in the form of convergence of finite partial products. -/
theorem riemannZeta_eulerProduct (hs : 1 < s.re) :
    Tendsto (fun n : â„• â†¦ âˆ p âˆˆ primesBelow n, (1 - (p : â„‚) ^ (-s))â»Â¹) atTop
      (ð“ (riemannZeta s)) := by
  rw [â† tsum_riemannZetaSummand hs]
  apply eulerProduct_completely_multiplicative <| summable_riemannZetaSummand hs

open scoped LSeries.notation

/-- The Euler product for Dirichlet L-series, valid for `s.re > 1`.
This version is stated in terms of `HasProd`. -/
theorem DirichletCharacter.LSeries_eulerProduct_hasProd {N : â„•} (Ï‡ : DirichletCharacter â„‚ N)
    (hs : 1 < s.re) :
    HasProd (fun p : Primes â†¦ (1 - Ï‡ p * (p : â„‚) ^ (-s))â»Â¹) (L â†—Ï‡ s) := by
  rw [â† tsum_dirichletSummand Ï‡ hs]
  convert eulerProduct_completely_multiplicative_hasProd <| summable_dirichletSummand Ï‡ hs

/-- The Euler product for Dirichlet L-series, valid for `s.re > 1`.
This version is stated in terms of `tprod`. -/
theorem DirichletCharacter.LSeries_eulerProduct_tprod {N : â„•} (Ï‡ : DirichletCharacter â„‚ N)
    (hs : 1 < s.re) :
    âˆ' p : Primes, (1 - Ï‡ p * (p : â„‚) ^ (-s))â»Â¹ = L â†—Ï‡ s :=
  (DirichletCharacter.LSeries_eulerProduct_hasProd Ï‡ hs).tprod_eq

/-- The Euler product for Dirichlet L-series, valid for `s.re > 1`.
This version is stated in the form of convergence of finite partial products. -/
theorem DirichletCharacter.LSeries_eulerProduct {N : â„•} (Ï‡ : DirichletCharacter â„‚ N)
    (hs : 1 < s.re) :
    Tendsto (fun n : â„• â†¦ âˆ p âˆˆ primesBelow n, (1 - Ï‡ p * (p : â„‚) ^ (-s))â»Â¹) atTop
      (ð“ (L â†—Ï‡ s)) := by
  rw [â† tsum_dirichletSummand Ï‡ hs]
  apply eulerProduct_completely_multiplicative <| summable_dirichletSummand Ï‡ hs

open LSeries

/-- A variant of the Euler product for Dirichlet L-series. -/
theorem DirichletCharacter.LSeries_eulerProduct_exp_log {N : â„•} (Ï‡ : DirichletCharacter â„‚ N)
    {s : â„‚} (hs : 1 < s.re) :
    exp (âˆ‘' p : Nat.Primes, -log (1 - Ï‡ p * p ^ (-s))) = L â†—Ï‡ s := by
  let f := dirichletSummandHom Ï‡ <| ne_zero_of_one_lt_re hs
  have h n : term â†—Ï‡ s n = f n := by
    rcases eq_or_ne n 0 with rfl | hn
    Â· simp only [term_zero, map_zero]
    Â· simp only [ne_eq, hn, not_false_eq_true, term_of_ne_zero, div_eq_mul_inv,
        dirichletSummandHom, cpow_neg, MonoidWithZeroHom.coe_mk, ZeroHom.coe_mk, f]
  simpa only [LSeries, h]
    using exp_tsum_primes_log_eq_tsum (f := f) <| summable_dirichletSummand Ï‡ hs

open DirichletCharacter

/-- A variant of the Euler product for the L-series of `Î¶`. -/
theorem ArithmeticFunction.LSeries_zeta_eulerProduct_exp_log {s : â„‚} (hs : 1 < s.re) :
    exp (âˆ‘' p : Nat.Primes, -Complex.log (1 - p ^ (-s))) = L 1 s := by
  convert modOne_eq_one (R := â„‚) â–¸
    DirichletCharacter.LSeries_eulerProduct_exp_log (1 : DirichletCharacter â„‚ 1) hs using 7
  rw [MulChar.one_apply <| isUnit_of_subsingleton _, one_mul]

/-- A variant of the Euler product for the Riemann zeta function. -/
theorem riemannZeta_eulerProduct_exp_log {s : â„‚} (hs : 1 < s.re) :
    exp (âˆ‘' p : Nat.Primes, -Complex.log (1 - p ^ (-s))) = riemannZeta s :=
  LSeries_one_eq_riemannZeta hs â–¸ ArithmeticFunction.LSeries_zeta_eulerProduct_exp_log hs

/-!
### Changing the level of a Dirichlet `L`-series
-/

/-- If `Ï‡` is a Dirichlet character and its level `M` divides `N`, then we obtain the L-series
of `Ï‡` considered as a Dirichlet character of level `N` from the L-series of `Ï‡` by multiplying
with `âˆ p âˆˆ N.primeFactors, (1 - Ï‡ p * p ^ (-s))`. -/
lemma DirichletCharacter.LSeries_changeLevel {M N : â„•} [NeZero N]
    (hMN : M âˆ£ N) (Ï‡ : DirichletCharacter â„‚ M) {s : â„‚} (hs : 1 < s.re) :
    LSeries â†—(changeLevel hMN Ï‡) s =
      LSeries â†—Ï‡ s * âˆ p âˆˆ N.primeFactors, (1 - Ï‡ p * p ^ (-s)) := by
  rw [prod_eq_tprod_mulIndicator, â† DirichletCharacter.LSeries_eulerProduct_tprod _ hs,
    â† DirichletCharacter.LSeries_eulerProduct_tprod _ hs]
  -- convert to a form suitable for `tprod_subtype`
  have (f : Primes â†’ â„‚) : âˆ' (p : Primes), f p = âˆ' (p : â†‘{p : â„• | p.Prime}), f p := rfl
  rw [this, tprod_subtype _ fun p : â„• â†¦ (1 - (changeLevel hMN Ï‡) p * p ^ (-s))â»Â¹,
    this, tprod_subtype _ fun p : â„• â†¦ (1 - Ï‡ p * p ^ (-s))â»Â¹, â† Multipliable.tprod_mul]
  rotate_left -- deal with convergence goals first
  Â· exact multipliable_subtype_iff_mulIndicator.mp
      (DirichletCharacter.LSeries_eulerProduct_hasProd Ï‡ hs).multipliable
  Â· exact multipliable_subtype_iff_mulIndicator.mp Multipliable.of_finite
  Â· congr 1 with p
    simp only [Set.mulIndicator_apply, Set.mem_setOf_eq, Finset.mem_coe, Nat.mem_primeFactors,
      ne_eq, mul_ite, ite_mul, one_mul, mul_one]
    by_cases h : p.Prime; swap
    Â· simp only [h, false_and, if_false]
    simp only [h, true_and, if_true]
    by_cases hp' : p âˆ£ N; swap
    Â· simp only [hp', false_and, â†“reduceIte, inv_inj, sub_right_inj, mul_eq_mul_right_iff,
        cpow_eq_zero_iff, Nat.cast_eq_zero, h.ne_zero, ne_eq, neg_eq_zero, or_false]
      have hq : IsUnit (p : ZMod N) := (ZMod.isUnit_prime_iff_not_dvd h).mpr hp'
      simp only [hq.unit_spec â–¸ DirichletCharacter.changeLevel_eq_cast_of_dvd Ï‡ hMN hq.unit,
        ZMod.cast_natCast hMN]
    Â· simp only [hp', NeZero.ne N, not_false_eq_true, and_self, â†“reduceIte]
      have : Â¬IsUnit (p : ZMod N) := by rwa [ZMod.isUnit_prime_iff_not_dvd h, not_not]
      rw [MulChar.map_nonunit _ this, zero_mul, sub_zero, inv_one]
      refine (inv_mul_cancelâ‚€ ?_).symm
      rw [sub_ne_zero, ne_comm]
      -- Remains to show `Ï‡ p * p ^ (-s) â‰  1`. We show its norm is strictly `< 1`.
      apply_fun (â€–Â·â€–)
      simp only [norm_mul, norm_one]
      have ha : â€–Ï‡ pâ€– â‰¤ 1 := Ï‡.norm_le_one p
      have hb : â€–(p : â„‚) ^ (-s)â€– â‰¤ 1 / 2 := norm_prime_cpow_le_one_half âŸ¨p, hâŸ© hs
      exact ((mul_le_mul ha hb (norm_nonneg _) zero_le_one).trans_lt (by norm_num)).ne
