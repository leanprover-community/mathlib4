/-
Copyright (c) 2023 Michael Stoll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Stoll
-/
import Mathlib.NumberTheory.SmoothNumbers
import Mathlib.Analysis.SpecificLimits.Normed
import Mathlib.Analysis.Normed.Field.InfiniteSum

/-!
# Euler Products

The main results in this file are `EulerProduct.euler_product`, which says that
if `f : â„• â†’ F` is norm-summable, where `F` is a complete normed field and `f` is
multiplicative on coprime arguments with `f 0 = 0`, then
`âˆ p in primesBelow N, âˆ‘' e : â„•, f (p^e)` tends to `âˆ‘' n, f n` as `N` tends to infinity,
and `EulerProduct.euler_product_multiplicative`, which states that
`âˆ p in primesBelow N, (1 - f p)â»Â¹` tends to `âˆ‘' n, f n` as `N` tends to infinity,
when `f` is completely multiplicative (implemented as `f : â„• â†’*â‚€ F`).

An intermediate step is `EulerProduct.summable_and_hasSum_smoothNumbers_prod_primesBelow_tsum`
(and its variant `EulerProduct.summable_and_hasSum_smoothNumbers_prod_primesBelow_tsum_geometric`),
which relates the finite product over primes to the sum of `f n` over `N`-smooth `n`.

## Tags

Euler product
-/

variable {F : Type*} [NormedField F] [CompleteSpace F]

/-- If `f` is multiplicative and summable, then its values at natural numbers `> 1`
have norm strictly less than `1`. -/
lemma Summable.norm_lt_one {f : â„• â†’* F} (hsum : Summable f) {p : â„•} (hp : 1 < p) :
    â€–f pâ€– < 1 := by
  refine summable_geometric_iff_norm_lt_1.mp ?_
  simp_rw [â† map_pow]
  exact hsum.comp_injective <| Nat.pow_right_injective hp


open scoped Topology

open BigOperators

-- We work with series indexed by the natural numbers
-- and with terms in a complete normed field `F`.
variable {F : Type*} [NormedField F] [CompleteSpace F] {f : â„• â†’ F}

-- We assume that `f` is *multiplicative* in the sense of arithmetic functions,
-- i.e., multiplicative on coprime elements.
-- The condition `f 1 = 1` is then equivalent to `f 1 â‰  0`, but more convenient to use.
variable (hfâ‚ : f 1 = 1) (hmul : âˆ€ {m n}, Nat.Coprime m n â†’ f (m * n) = f m * f n)

lemma Nat.map_prime_pow_mul {p : â„•} (hp : p.Prime) (e : â„•) {m : p.smoothNumbers} :
    f (p ^ e * m) = f (p ^ e) * f m :=
  hmul <| Nat.Coprime.pow_left _ <| hp.smoothNumbers_coprime <| Subtype.mem m

namespace EulerProduct

open Nat in
/-- We relate a finite product over primes to an infinite sum over smooth numbers. -/
lemma summable_and_hasSum_smoothNumbers_prod_primesBelow_tsum
    (hsum : âˆ€ {p : â„•}, p.Prime â†’ Summable (fun n : â„• â†¦ â€–f (p ^ n)â€–)) (N : â„•) :
    Summable (fun m : N.smoothNumbers â†¦ â€–f mâ€–) âˆ§
      HasSum (fun m : N.smoothNumbers â†¦ f m) (âˆ p in N.primesBelow, âˆ‘' (n : â„•), f (p ^ n)) := by
  induction' N with N ih
  Â· rw [smoothNumbers_zero, primesBelow_zero, Finset.prod_empty]
    exact âŸ¨Set.Finite.summable (Set.finite_singleton 1) (â€–f Â·â€–), hfâ‚ â–¸ hasSum_singleton 1 fâŸ©
  Â· rw [primesBelow_succ]
    split_ifs with hN
    Â· constructor
      Â· simp_rw [â† (equivProdNatSmoothNumbers hN).summable_iff, Function.comp_def,
                 equivProdNatSmoothNumbers_apply', map_prime_pow_mul hmul hN, norm_mul]
        apply summable_mul_of_summable_norm (f := fun (x : â„•) â†¦ â€–f (N ^ x)â€–)
          (g := fun (x : N.smoothNumbers) â†¦ â€–f x.valâ€–) <;>
          simp_rw [norm_norm]
        exacts [hsum hN, ih.1]
      Â· simp_rw [Finset.prod_insert (not_mem_primesBelow N),
                 â† (equivProdNatSmoothNumbers hN).hasSum_iff, Function.comp_def,
                 equivProdNatSmoothNumbers_apply', map_prime_pow_mul hmul hN]
        apply (hsum hN).of_norm.hasSum.mul ih.2
        -- `exact summable_mul_of_summable_norm (hsum hN) ih.1` gives a time-out
        convert summable_mul_of_summable_norm (hsum hN) ih.1
    Â· rwa [smoothNumbers_succ hN]

open Nat in
/-- Given a (completely) multiplicative function `f : â„• â†’ F`, where `F` is a normed field,
such that `â€–f pâ€– < 1` for all primes `p`, we can express the sum of `f n` over all `N`-smooth
positive integers `n` as a product of `(1 - f p)â»Â¹` over the primes `p < N`. At the same time,
we show that the sum involved converges absolutely. -/
lemma summable_and_hasSum_smoothNumbers_prod_primesBelow_geometric {f : â„• â†’* F}
    (h : âˆ€ {p : â„•}, p.Prime â†’ â€–f pâ€– < 1) (N : â„•) :
    Summable (fun m : N.smoothNumbers â†¦ â€–f mâ€–) âˆ§
      HasSum (fun m : N.smoothNumbers â†¦ f m) (âˆ p in N.primesBelow, (1 - f p)â»Â¹) := by
  have hmul {m n} (_ : Nat.Coprime m n) := f.map_mul m n
  convert summable_and_hasSum_smoothNumbers_prod_primesBelow_tsum f.map_one hmul ?_ N with M hM <;>
    simp_rw [map_pow]
  Â· exact (tsum_geometric_of_norm_lt_1 <| h <| prime_of_mem_primesBelow hM).symm
  Â· intro p hp
    simp_rw [norm_pow]
    exact summable_geometric_iff_norm_lt_1.mpr <| (norm_norm (f p)).symm â–¸ h hp

-- We now assume in addition that `f` is norm-summable.
variable (hsum : Summable (â€–f Â·â€–))

/-- A version of `EulerProduct.summable_and_hasSum_smoothNumbers_prod_primesBelow_tsum`
in terms of the value of the series. -/
lemma prod_primesBelow_tsum_eq_tsum_smoothNumbers (N : â„•) :
    âˆ p in N.primesBelow, âˆ‘' (n : â„•), f (p ^ n) = âˆ‘' m : N.smoothNumbers, f m :=
  (summable_and_hasSum_smoothNumbers_prod_primesBelow_tsum hfâ‚ hmul
    (fun hp â†¦ hsum.comp_injective <| Nat.pow_right_injective hp.one_lt) _).2.tsum_eq.symm

/-- A version of `EulerProduct.summable_and_hasSum_smoothNumbers_prod_primesBelow_geometric`
in terms of the value of the series. -/
lemma prod_primesBelow_geometric_eq_tsum_smoothNumbers {f : â„• â†’* F} (hsum : Summable f) (N : â„•) :
    âˆ p in N.primesBelow, (1 - f p)â»Â¹ = âˆ‘' m : N.smoothNumbers, f m := by
  refine (summable_and_hasSum_smoothNumbers_prod_primesBelow_geometric ?_ N).2.tsum_eq.symm
  exact fun {_} hp â†¦ hsum.norm_lt_one hp.one_lt

/-- The following statement says that summing over `N`-smooth numbers
for large enough `N` gets us arbitrarily close to the sum over all natural numbers
(assuming `f` is norm-summable and `f 0 = 0`; the latter since `0` is not smooth). -/
lemma norm_tsum_smoothNumbers_sub_tsum_lt (hsum : Summable f) (hfâ‚€ : f 0 = 0)
    {Îµ : â„} (Îµpos : 0 < Îµ) :
    âˆƒ Nâ‚€ : â„•, âˆ€ N â‰¥ Nâ‚€, â€–(âˆ‘' m : â„•, f m) - (âˆ‘' m : N.smoothNumbers, f m)â€– < Îµ := by
  obtain âŸ¨Nâ‚€, hNâ‚€âŸ© :=
    summable_iff_nat_tsum_vanishing.mp hsum (Metric.ball 0 Îµ) <| Metric.ball_mem_nhds 0 Îµpos
  simp_rw [mem_ball_zero_iff] at hNâ‚€
  refine âŸ¨Nâ‚€, fun N hNâ‚ â†¦ ?_âŸ©
  convert hNâ‚€ _ <| N.smoothNumbers_compl.trans fun _ â†¦ hNâ‚.le.trans
  simp_rw [â† tsum_subtype_add_tsum_subtype_compl hsum N.smoothNumbers,
    add_sub_cancel', tsum_eq_tsum_diff_singleton (N.smoothNumbers)á¶œ hfâ‚€]

open Filter Nat in
/-- The *Euler Product* for multiplicative (on coprime arguments) functions.
If `f : â„• â†’ F`, where `F` is a complete normed field, `f 0 = 0`,
`f 1 = 1`, `f` is multiplicative on coprime arguments,
and `â€–f Â·â€–` is summable, then `âˆ' p : {p : â„• | p.Prime}, âˆ‘' e, f (p ^ e) = âˆ‘' n, f n`.
Since there are no infinite products yet in Mathlib, we state it in the form of
convergence of finite partial products. -/
-- TODO: Change to use `âˆ'` once infinite products are in Mathlib
theorem euler_product (hfâ‚€ : f 0 = 0) :
    Tendsto (fun n : â„• â†¦ âˆ p in primesBelow n, âˆ‘' e, f (p ^ e)) atTop (ğ“ (âˆ‘' n, f n)) := by
  rw [Metric.tendsto_nhds]
  intro Îµ Îµpos
  simp only [Finset.mem_range, eventually_atTop, ge_iff_le]
  obtain âŸ¨Nâ‚€, hNâ‚€âŸ© := norm_tsum_smoothNumbers_sub_tsum_lt hsum.of_norm hfâ‚€ Îµpos
  use Nâ‚€
  convert hNâ‚€ using 3 with m
  rw [dist_eq_norm, norm_sub_rev]
  congr 2
  exact prod_primesBelow_tsum_eq_tsum_smoothNumbers hfâ‚ hmul hsum m

open Filter Nat in
/-- The *Euler Product* for completely multiplicative functions.
If `f : â„• â†’*â‚€ F`, where `F` is a complete normed field
and `â€–f Â·â€–` is summable, then `âˆ' p : {p : â„• | p.Prime}, (1 - f p)â»Â¹ = âˆ‘' n, f n`.
Since there are no infinite products yet in Mathlib, we state it in the form of
convergence of finite partial products. -/
-- TODO: Change to use `âˆ'` once infinite products are in Mathlib
theorem euler_product_multiplicative {f : â„• â†’*â‚€ F} (hsum : Summable fun x => â€–f xâ€–) :
    Tendsto (fun n : â„• â†¦ âˆ p in primesBelow n, (1 - f p)â»Â¹) atTop (ğ“ (âˆ‘' n, f n)) := by
  convert euler_product f.map_one (fun {m n} _ â†¦ f.map_mul m n) hsum f.map_zero with N p hN
  simp_rw [map_pow]
  refine (tsum_geometric_of_norm_lt_1 <| summable_geometric_iff_norm_lt_1.mp ?_).symm
  refine Summable.of_norm ?_
  convert hsum.comp_injective <| pow_right_injective (prime_of_mem_primesBelow hN).one_lt
  simp only [norm_pow, Function.comp_apply, map_pow]

end EulerProduct
