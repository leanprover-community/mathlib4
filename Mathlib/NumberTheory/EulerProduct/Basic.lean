/-
Copyright (c) 2023 Michael Stoll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Stoll
-/
import Mathlib.Analysis.Normed.Field.InfiniteSum
import Mathlib.Analysis.SpecificLimits.Normed
import Mathlib.NumberTheory.ArithmeticFunction
import Mathlib.NumberTheory.SmoothNumbers

/-!
# Euler Products

The main result in this file is `EulerProduct.eulerProduct`, which says that
if `f : â„• â†’ R` is norm-summable, where `R` is a complete normed commutative ring and `f` is
multiplicative on coprime arguments with `f 0 = 0`, then
`âˆ p in primesBelow N, âˆ‘' e : â„•, f (p^e)` tends to `âˆ‘' n, f n` as `N` tends to infinity.
`Nat.ArithmeticFunction.IsMultiplicative.eulerProduct` is a version of
`EulerProduct.eulerProduct` for multiplicative arithmetic functions in the sense of
`Nat.ArithmeticFunction.IsMultiplicative`.

There is also a version `EulerProduct.eulerProduct_completely_multiplicative`, which states that
`âˆ p in primesBelow N, (1 - f p)â»Â¹` tends to `âˆ‘' n, f n` as `N` tends to infinity,
when `f` is completely multiplicative with values in a complete normed field `F`
(implemented as `f : â„• â†’*â‚€ F`).

An intermediate step is `EulerProduct.summable_and_hasSum_smoothNumbers_prod_primesBelow_tsum`
(and its variant `EulerProduct.summable_and_hasSum_smoothNumbers_prod_primesBelow_tsum_geometric`),
which relates the finite product over primes to the sum of `f n` over `N`-smooth `n`.

## Tags

Euler product
-/

/-- If `f` is multiplicative and summable, then its values at natural numbers `> 1`
have norm strictly less than `1`. -/
lemma Summable.norm_lt_one {F : Type*} [NormedField F] [CompleteSpace F] {f : â„• â†’* F}
    (hsum : Summable f) {p : â„•} (hp : 1 < p) :
    â€–f pâ€– < 1 := by
  refine summable_geometric_iff_norm_lt_1.mp ?_
  simp_rw [â† map_pow]
  exact hsum.comp_injective <| Nat.pow_right_injective hp

open scoped Topology

open Nat BigOperators

namespace EulerProduct

section General

/-!
### General Euler Products

In this section we consider multiplicative (on coprime arguments) functions `f : â„• â†’ R`,
where `R` is a complete normed commutative ring. The main result is `EulerProduct.eulerProduct`.
-/

variable {R : Type*} [NormedCommRing R] [CompleteSpace R] {f : â„• â†’ R}
variable (hfâ‚ : f 1 = 1) (hmul : âˆ€ {m n}, Nat.Coprime m n â†’ f (m * n) = f m * f n)

/-- We relate a finite product over primes to an infinite sum over smooth numbers. -/
lemma summable_and_hasSum_smoothNumbers_prod_primesBelow_tsum
    (hsum : âˆ€ {p : â„•}, p.Prime â†’ Summable (fun n : â„• â†¦ â€–f (p ^ n)â€–)) (N : â„•) :
    Summable (fun m : N.smoothNumbers â†¦ â€–f mâ€–) âˆ§
      HasSum (fun m : N.smoothNumbers â†¦ f m) (âˆ p in N.primesBelow, âˆ‘' (n : â„•), f (p ^ n)) := by
  induction' N with N ih
  Â· rw [smoothNumbers_zero, primesBelow_zero, Finset.prod_empty]
    exact âŸ¨(Set.finite_singleton 1).summable (â€–f Â·â€–), hfâ‚ â–¸ hasSum_singleton 1 fâŸ©
  Â· rw [primesBelow_succ]
    split_ifs with hN
    Â· constructor
      Â· simp_rw [â† (equivProdNatSmoothNumbers hN).summable_iff, Function.comp_def,
                 equivProdNatSmoothNumbers_apply', map_prime_pow_mul hmul hN]
        refine Summable.of_nonneg_of_le (fun _ â†¦ norm_nonneg _) (fun _ â†¦ norm_mul_le ..) ?_
        apply summable_mul_of_summable_norm (f := fun (x : â„•) â†¦ â€–f (N ^ x)â€–)
          (g := fun (x : N.smoothNumbers) â†¦ â€–f x.valâ€–) <;>
          simp_rw [norm_norm]
        exacts [hsum hN, ih.1]
      Â· simp_rw [Finset.prod_insert (not_mem_primesBelow N),
                 â† (equivProdNatSmoothNumbers hN).hasSum_iff, Function.comp_def,
                 equivProdNatSmoothNumbers_apply', map_prime_pow_mul hmul hN]
        apply (hsum hN).of_norm.hasSum.mul ih.2
        -- `exact summable_mul_of_summable_norm (hsum hN) ih.1` gives a time-out
        convert summable_mul_of_summable_norm (hsum hN) ih.1
    Â· rwa [smoothNumbers_succ hN]

/-- A version of `EulerProduct.summable_and_hasSum_smoothNumbers_prod_primesBelow_tsum`
in terms of the value of the series. -/
lemma prod_primesBelow_tsum_eq_tsum_smoothNumbers (hsum : Summable (â€–f Â·â€–)) (N : â„•) :
    âˆ p in N.primesBelow, âˆ‘' (n : â„•), f (p ^ n) = âˆ‘' m : N.smoothNumbers, f m :=
  (summable_and_hasSum_smoothNumbers_prod_primesBelow_tsum hfâ‚ hmul
    (fun hp â†¦ hsum.comp_injective <| Nat.pow_right_injective hp.one_lt) _).2.tsum_eq.symm

/-- The following statement says that summing over `N`-smooth numbers
for large enough `N` gets us arbitrarily close to the sum over all natural numbers
(assuming `f` is norm-summable and `f 0 = 0`; the latter since `0` is not smooth). -/
lemma norm_tsum_smoothNumbers_sub_tsum_lt (hsum : Summable f) (hfâ‚€ : f 0 = 0)
    {Îµ : â„} (Îµpos : 0 < Îµ) :
    âˆƒ Nâ‚€ : â„•, âˆ€ N â‰¥ Nâ‚€, â€–(âˆ‘' m : â„•, f m) - (âˆ‘' m : N.smoothNumbers, f m)â€– < Îµ := by
  obtain âŸ¨Nâ‚€, hNâ‚€âŸ© :=
    summable_iff_nat_tsum_vanishing.mp hsum (Metric.ball 0 Îµ) <| Metric.ball_mem_nhds 0 Îµpos
  simp_rw [mem_ball_zero_iff] at hNâ‚€
  refine âŸ¨Nâ‚€, fun N hNâ‚ â†¦ ?_âŸ©
  convert hNâ‚€ _ <| N.smoothNumbers_compl.trans fun _ â†¦ hNâ‚.le.trans
  simp_rw [â† tsum_subtype_add_tsum_subtype_compl hsum N.smoothNumbers,
    add_sub_cancel', tsum_eq_tsum_diff_singleton (N.smoothNumbers)á¶œ hfâ‚€]

open Filter

/-- The *Euler Product* for multiplicative (on coprime arguments) functions.
If `f : â„• â†’ R`, where `R` is a complete normed commutative ring, `f 0 = 0`,
`f 1 = 1`, `f` is multiplicative on coprime arguments,
and `â€–f Â·â€–` is summable, then `âˆ' p : {p : â„• | p.Prime}, âˆ‘' e, f (p ^ e) = âˆ‘' n, f n`.
Since there are no infinite products yet in Mathlib, we state it in the form of
convergence of finite partial products. -/
-- TODO: Change to use `âˆ'` once infinite products are in Mathlib
theorem eulerProduct (hsum : Summable (â€–f Â·â€–)) (hfâ‚€ : f 0 = 0) :
    Tendsto (fun n : â„• â†¦ âˆ p in primesBelow n, âˆ‘' e, f (p ^ e)) atTop (ğ“ (âˆ‘' n, f n)) := by
  rw [Metric.tendsto_nhds]
  intro Îµ Îµpos
  simp only [Finset.mem_range, eventually_atTop, ge_iff_le]
  obtain âŸ¨Nâ‚€, hNâ‚€âŸ© := norm_tsum_smoothNumbers_sub_tsum_lt hsum.of_norm hfâ‚€ Îµpos
  use Nâ‚€
  convert hNâ‚€ using 3 with m
  rw [dist_eq_norm, norm_sub_rev, prod_primesBelow_tsum_eq_tsum_smoothNumbers hfâ‚ hmul hsum m]

/-- The *Euler Product* for a multiplicative arithmetic function `f` with values in a
complete normed commutative ring `R`: if `â€–f Â·â€–` is summable, then
`âˆ' p : {p : â„• | p.Prime}, âˆ‘' e, f (p ^ e) = âˆ‘' n, f n`.
Since there are no infinite products yet in Mathlib, we state it in the form of
convergence of finite partial products. -/
-- TODO: Change to use `âˆ'` once infinite products are in Mathlib
nonrec theorem _root_.Nat.ArithmeticFunction.IsMultiplicative.eulerProduct
    {f : ArithmeticFunction R} (hf : f.IsMultiplicative) (hsum : Summable (â€–f Â·â€–)) :
    Tendsto (fun n : â„• â†¦ âˆ p in primesBelow n, âˆ‘' e, f (p ^ e)) atTop (ğ“ (âˆ‘' n, f n)) :=
  eulerProduct hf.1 hf.2 hsum f.map_zero

end General

section CompletelyMultiplicative

/-!
### Euler Products for completely multiplicative functions

We now assume that `f` is completely multiplicative and has values in a complete normed field `F`.
Then we can use the formula for geometric series to simplify the statement. This leads to
`EulerProduct.eulerProduct_completely_multiplicative`.
-/

variable {F : Type*} [NormedField F] [CompleteSpace F]

/-- Given a (completely) multiplicative function `f : â„• â†’ F`, where `F` is a normed field,
such that `â€–f pâ€– < 1` for all primes `p`, we can express the sum of `f n` over all `N`-smooth
positive integers `n` as a product of `(1 - f p)â»Â¹` over the primes `p < N`. At the same time,
we show that the sum involved converges absolutely. -/
lemma summable_and_hasSum_smoothNumbers_prod_primesBelow_geometric {f : â„• â†’* F}
    (h : âˆ€ {p : â„•}, p.Prime â†’ â€–f pâ€– < 1) (N : â„•) :
    Summable (fun m : N.smoothNumbers â†¦ â€–f mâ€–) âˆ§
      HasSum (fun m : N.smoothNumbers â†¦ f m) (âˆ p in N.primesBelow, (1 - f p)â»Â¹) := by
  have hmul {m n} (_ : Nat.Coprime m n) := f.map_mul m n
  convert summable_and_hasSum_smoothNumbers_prod_primesBelow_tsum f.map_one hmul ?_ N with M hM <;>
    simp_rw [map_pow]
  Â· exact (tsum_geometric_of_norm_lt_1 <| h <| prime_of_mem_primesBelow hM).symm
  Â· intro p hp
    refine Summable.of_nonneg_of_le (fun _ â†¦ norm_nonneg _) (fun _ â†¦ norm_pow_le ..) ?_
    exact summable_geometric_iff_norm_lt_1.mpr <| (norm_norm (f p)).symm â–¸ h hp

/-- A version of `EulerProduct.summable_and_hasSum_smoothNumbers_prod_primesBelow_geometric`
in terms of the value of the series. -/
lemma prod_primesBelow_geometric_eq_tsum_smoothNumbers {f : â„• â†’* F} (hsum : Summable f) (N : â„•) :
    âˆ p in N.primesBelow, (1 - f p)â»Â¹ = âˆ‘' m : N.smoothNumbers, f m := by
  refine (summable_and_hasSum_smoothNumbers_prod_primesBelow_geometric ?_ N).2.tsum_eq.symm
  exact fun {_} hp â†¦ hsum.norm_lt_one hp.one_lt

open Filter in
/-- The *Euler Product* for completely multiplicative functions.
If `f : â„• â†’*â‚€ F`, where `F` is a complete normed field
and `â€–f Â·â€–` is summable, then `âˆ' p : {p : â„• | p.Prime}, (1 - f p)â»Â¹ = âˆ‘' n, f n`.
Since there are no infinite products yet in Mathlib, we state it in the form of
convergence of finite partial products. -/
-- TODO: Change to use `âˆ'` once infinite products are in Mathlib
theorem eulerProduct_completely_multiplicative {f : â„• â†’*â‚€ F} (hsum : Summable fun x => â€–f xâ€–) :
    Tendsto (fun n : â„• â†¦ âˆ p in primesBelow n, (1 - f p)â»Â¹) atTop (ğ“ (âˆ‘' n, f n)) := by
  convert eulerProduct f.map_one (fun {m n} _ â†¦ f.map_mul m n) hsum f.map_zero with N p hN
  simp_rw [map_pow]
  refine (tsum_geometric_of_norm_lt_1 <| summable_geometric_iff_norm_lt_1.mp ?_).symm
  refine Summable.of_norm ?_
  convert hsum.comp_injective <| Nat.pow_right_injective (prime_of_mem_primesBelow hN).one_lt
  simp only [norm_pow, Function.comp_apply, map_pow]

end CompletelyMultiplicative

end EulerProduct
