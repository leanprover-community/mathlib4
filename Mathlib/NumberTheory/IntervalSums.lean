/-
Copyright (c) 2025 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/

import Mathlib.Algebra.Order.Ring.Star
import Mathlib.Algebra.Order.Archimedean.Basic
import Mathlib.Algebra.CharP.Defs
import Mathlib.Topology.Algebra.Monoid.Defs
import Mathlib.Topology.Algebra.InfiniteSum.Defs
import Mathlib.Data.Int.Interval
import Mathlib.Order.Filter.AtTopBot.Finset

/-!
# Sums over symmetric integer intervals

This file contains some lemmas about sums over symmetric integer intervals `Ixx -N N` used in the
definition of the Eisenstein series `E2`. In particular we define `IccFilter`, `IcoFilter`,
`IocFilter` and `IooFilter` as `SummationFilter`s on `â„¤` corresponding to the intervals
`Icc -N N`, `Ico -N N`, `Ioc -N N` and `Ioo -N N` respectively.
We also prove that these filters are all `NeBot` and `LeAtTop`.

-/

open Finset Topology

--Should this go elsewhere?
lemma Finset.Icc_succ_succ (n : â„•) : Icc (-(n + 1) : â„¤) (n + 1) = Icc (-n : â„¤) n âˆª
  {(-(n + 1) : â„¤), (n + 1 : â„¤)} := by
  refine Finset.ext_iff.mpr ?_
  intro a
  simp only [neg_add_rev, Int.reduceNeg, mem_Icc, add_neg_le_iff_le_add, union_insert,
    union_singleton, mem_insert]
  omega

lemma Finset.sum_Icc_of_even_eq_range {Î± : Type*} [CommRing Î±] {f : â„¤ â†’ Î±} (hf : âˆ€ n, f n = f (-n))
    (N : â„•) : âˆ‘ m âˆˆ Icc (-N : â„¤) N, f m = 2 * âˆ‘ m âˆˆ range (N + 1), f m - f 0 := by
  induction N with
  | zero => simp [two_mul]
  | succ N ih =>
    have := Icc_succ_succ N
    simp only [neg_add_rev, Int.reduceNeg, Nat.cast_add, Nat.cast_one] at *
    rw [this, sum_union (by simp), sum_pair (by omega), ih]
    nth_rw 2 [sum_range_succ]
    grind

@[to_additive]
lemma Finset.prod_Icc_eq_prod_Ico_succ {Î± : Type*} [CommMonoid Î±] (f : â„¤ â†’ Î±) {l u : â„¤}
    (h : l â‰¤ u) : âˆ m âˆˆ Icc l u, f m = (âˆ m âˆˆ Ico l u, f m) * f u := by
  simp only [Icc_eq_cons_Ico h, cons_eq_insert, mem_Ico, lt_self_iff_false, and_false,
    not_false_eq_true, prod_insert, mul_comm]

lemma Finset.sum_Icc_add_endpoints {R : Type*} [AddCommGroup R] (f : â„¤ â†’ R) {N : â„•} (hn : 1 â‰¤ N) :
    âˆ‘ m âˆˆ Icc (-N : â„¤) N, f m = f N + f (-N : â„¤) + âˆ‘ m âˆˆ Icc (-(N - 1) : â„¤) (N - 1), f m := by
  induction N
  Â· grind
  Â· zify
    rw [Icc_succ_succ, sum_union (by simp)]
    grind

section IntervalFilters

open TopologicalSpace Filter Function Finset SummationFilter

lemma Finset.tendsto_Icc_atTop_atTop : Tendsto (fun N : â„• â†¦ Icc (-N : â„¤) N) atTop atTop :=
  tendsto_atTop_finset_of_monotone (fun _ _ _ â†¦ Icc_subset_Icc (by gcongr) (by gcongr))
  (fun x â†¦ âŸ¨x.natAbs, by simp [le_abs, neg_le]âŸ©)

lemma Finset.tendsto_Ico_atTop_atTop : Tendsto (fun N : â„• â†¦ Ico (-N : â„¤) N) atTop atTop := by
  apply tendsto_atTop_finset_of_monotone (fun _ _ _ â†¦ Ico_subset_Ico (by omega) (by gcongr))
  exact fun x â†¦ âŸ¨x.natAbs + 1, by simpa using âŸ¨by apply le_trans _ (add_abs_nonneg x); omega,
    Int.lt_add_one_iff.mpr (le_abs_self x)âŸ© âŸ©

lemma Finset.tendsto_Ioc_atTop_atTop : Tendsto (fun N : â„• â†¦ Ioc (-N : â„¤) N) atTop atTop := by
  apply tendsto_atTop_finset_of_monotone (fun _ _ _ â†¦ Ioc_subset_Ioc (by omega) (by gcongr))
  exact fun x â†¦ âŸ¨x.natAbs + 1, by simpa using âŸ¨by apply le_trans _ (add_abs_nonneg x); omega,
    (Int.lt_add_one_iff.mpr (le_abs_self x)).leâŸ©âŸ©

lemma Finset.tendsto_Ioo_atTop_atTop : Tendsto (fun N : â„• â†¦ Ioo (-N : â„¤) N) atTop atTop := by
  apply tendsto_atTop_finset_of_monotone (fun _ _ _ â†¦ Ioo_subset_Ioo (by omega) (by gcongr))
  exact fun x â†¦ âŸ¨x.natAbs + 1, by simpa using âŸ¨by apply le_trans _ (add_abs_nonneg x); omega,
    (Int.lt_add_one_iff.mpr (le_abs_self x))âŸ©âŸ©

/-- The SummationFilter on `â„¤` corresponding to the symmetric intervals `Icc (-N) N`Â· -/
abbrev SummationFilter.SymmetricConditional : SummationFilter â„¤ where
  filter := atTop.map (fun N : â„• â†¦ Icc (-(N : â„¤)) N)

lemma SymmetricConditional_eq_Icc :
    SymmetricConditional.filter = atTop.map (fun N : â„• â†¦ Icc (-(N : â„¤)) N) := rfl

/-- The SummationFilter on `â„¤` corresponding to the intervals `Ico (-N) N`. -/
abbrev SummationFilter.IcoFilter : SummationFilter â„¤ where
  filter := atTop.map (fun N : â„• â†¦ Ico (-(N : â„¤)) N)

/-- The SummationFilter on `â„¤` corresponding to the intervals `Ioc (-N) N`. -/
abbrev SummationFilter.IocFilter : SummationFilter â„¤ where
  filter := atTop.map (fun N : â„• â†¦ Ioc (-(N : â„¤)) N)

lemma SymmetricConditional_eq_Ioo :
    SymmetricConditional.filter = atTop.map (fun N : â„• â†¦ Ioo (-(N : â„¤)) N) := by
  ext s
  constructor
  Â· simp only [Filter.mem_map, Filter.mem_atTop_sets, Set.mem_preimage]
    intro âŸ¨a, haâŸ©
    refine âŸ¨a + 1, fun b hb â†¦ ?_âŸ©
    convert ha (b - 1) (by grind) using 1
    ext x
    simp only [mem_Ioo, mem_Icc]
    grind
  Â· simp only [Filter.mem_map, Filter.mem_atTop_sets, Set.mem_preimage]
    intro âŸ¨a, haâŸ©
    refine âŸ¨a - 1, fun b hb â†¦ ?_âŸ©
    convert ha (b + 1) (by grind) using 1
    ext x
    simp only [mem_Icc, Nat.cast_add, Nat.cast_one, neg_add_rev, Int.reduceNeg, mem_Ioo,
      add_neg_lt_iff_lt_add]
    grind

instance SymmetricConditional_neBot : NeBot (atTop.map (fun N : â„• â†¦ Icc (-(N : â„¤)) N)) := by
  simp [Filter.NeBot.map]

instance IcoFilter_neBot : NeBot (atTop.map (fun N : â„• â†¦ Ico (-(N : â„¤)) N)) := by
  simp [Filter.NeBot.map]

instance IocFilter_neBot : NeBot (atTop.map (fun N : â„• â†¦ Ioc (-(N : â„¤)) N)) := by
  simp [Filter.NeBot.map]

lemma SymmetricConditional_le_atTop : atTop.map (fun N : â„• â†¦ Icc (-(N : â„¤)) N) â‰¤ atTop := by
  rw [@map_le_iff_le_comap, â† @tendsto_iff_comap]
  exact tendsto_Icc_atTop_atTop

lemma IcoFilter_le_atTop : atTop.map (fun N : â„• â†¦ Ico (-(N : â„¤)) N) â‰¤ atTop := by
  rw [@map_le_iff_le_comap, â† @tendsto_iff_comap]
  exact tendsto_Ico_atTop_atTop

lemma IocFilter_le_atTop : atTop.map (fun N : â„• â†¦ Ioc (-(N : â„¤)) N) â‰¤ atTop := by
  rw [@map_le_iff_le_comap, â† @tendsto_iff_comap]
  exact tendsto_Ioc_atTop_atTop

instance : (SymmetricConditional).NeBot := âŸ¨SymmetricConditional_neBotâŸ©

instance : (IcoFilter).NeBot := âŸ¨IcoFilter_neBotâŸ©

instance : (IocFilter).NeBot := âŸ¨IocFilter_neBotâŸ©

instance : (SymmetricConditional).LeAtTop where
  le_atTop := SymmetricConditional_le_atTop

instance : (IcoFilter).LeAtTop where
  le_atTop := IcoFilter_le_atTop

instance : (IocFilter).LeAtTop where
  le_atTop := IocFilter_le_atTop

variable {Î± : Type*} {f : â„¤ â†’ Î±} [CommGroup Î±] [TopologicalSpace Î±] [ContinuousMul Î±]

@[to_additive]
lemma multipliable_IcoFilter_of_multiplible_SymmetricConditional
    (hf : Multipliable f SymmetricConditional) (hf2 : Tendsto (fun N : â„• â†¦ (f N)â»Â¹) atTop (ð“ 1)) :
    Multipliable f IcoFilter := by
  have := (hf.hasProd)
  apply HasProd.multipliable (a := âˆ'[SymmetricConditional] (b : â„¤), f b)
  simp only [HasProd, tendsto_map'_iff] at *
  apply Filter.Tendsto_of_div_tendsto_one _ this
  conv =>
    enter [1, N]
    simp only [Pi.div_apply, comp_apply]
    rw [prod_Icc_eq_prod_Ico_succ _ (by omega)]
  simpa using hf2

@[to_additive]
lemma tprod_SymmetricConditional_eq_tprod_IcoFilter [T2Space Î±]
    (hf : Multipliable f SymmetricConditional) (hf2 : Tendsto (fun N : â„• â†¦ (f N)â»Â¹) atTop (ð“ 1)) :
    âˆ'[SymmetricConditional] b, f b = âˆ'[IcoFilter] b, f b := by
  have := (hf.hasProd)
  apply symm
  apply HasProd.tprod_eq
  simp only [HasProd, tendsto_map'_iff] at *
  apply Filter.Tendsto_of_div_tendsto_one _ this
  conv =>
    enter [1, N]
    simp only [Pi.div_apply, comp_apply]
    rw [prod_Icc_eq_prod_Ico_succ _ (by omega)]
  simpa using hf2

end IntervalFilters
