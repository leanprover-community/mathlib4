/-
Copyright (c) 2025 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/

import Mathlib.Algebra.Order.Ring.Star
import Mathlib.Algebra.Order.Archimedean.Basic
import Mathlib.Algebra.CharP.Defs
import Mathlib.Topology.Algebra.Monoid.Defs
import Mathlib.Topology.Algebra.InfiniteSum.Defs
import Mathlib.Data.Int.Interval
import Mathlib.Order.Filter.AtTopBot.Finset

/-!
# Sums over symmetric integer intervals

This file contains some lemmas about sums over symmetric integer intervals `Ixx -N N` used in the
definition of the Eisenstein series `E2`. In particular we define `IccFilter`, `IcoFilter`,
`IocFilter` and `IooFilter` as `SummationFilter`s on `â„¤` corresponding to the intervals
`Icc -N N`, `Ico -N N`, `Ioc -N N` and `Ioo -N N` respectively.
We also prove that these filters are all `NeBot` and `LeAtTop`.

-/

open Finset Topology

--Should this go elsewhere?
lemma Finset.Icc_succ_succ (n : â„•) : Icc (-(n + 1) : â„¤) (n + 1) = Icc (-n : â„¤) n âˆª
  {(-(n + 1) : â„¤), (n + 1 : â„¤)} := by
  refine Finset.ext_iff.mpr ?_
  intro a
  simp only [neg_add_rev, Int.reduceNeg, mem_Icc, add_neg_le_iff_le_add, union_insert,
    union_singleton, mem_insert]
  omega

lemma Finset.sum_Icc_of_even_eq_range {Î± : Type*} [CommRing Î±] {f : â„¤ â†’ Î±} (hf : âˆ€ n, f n = f (-n))
    (N : â„•) : âˆ‘ m âˆˆ Icc (-N : â„¤) N, f m = 2 * âˆ‘ m âˆˆ range (N + 1), f m - f 0 := by
  induction N with
  | zero => simp [two_mul]
  | succ N ih =>
    have := Icc_succ_succ N
    simp only [neg_add_rev, Int.reduceNeg, Nat.cast_add, Nat.cast_one] at *
    rw [this, sum_union (by simp), sum_pair (by omega), ih]
    nth_rw 2 [sum_range_succ]
    grind

@[to_additive]
lemma Finset.prod_Icc_eq_prod_Ico_succ {Î± : Type*} [CommMonoid Î±] (f : â„¤ â†’ Î±) {l u : â„¤}
    (h : l â‰¤ u) : âˆ m âˆˆ Icc l u, f m = (âˆ m âˆˆ Ico l u, f m) * f u := by
  simp only [Icc_eq_cons_Ico h, cons_eq_insert, mem_Ico, lt_self_iff_false, and_false,
    not_false_eq_true, prod_insert, mul_comm]

lemma Finset.sum_Icc_add_endpoints {R : Type*} [AddCommGroup R] (f : â„¤ â†’ R) {N : â„•} (hn : 1 â‰¤ N) :
    âˆ‘ m âˆˆ Icc (-N : â„¤) N, f m = f N + f (-N : â„¤) + âˆ‘ m âˆˆ Icc (-(N - 1) : â„¤) (N - 1), f m := by
  induction N
  Â· grind
  Â· zify
    rw [Icc_succ_succ, sum_union (by simp)]
    grind

section IntervalFilters

open TopologicalSpace Filter Function Finset SummationFilter

lemma Finset.tendsto_Icc_atTop_atTop : Tendsto (fun N : â„• â†¦ Icc (-N : â„¤) N) atTop atTop :=
  tendsto_atTop_finset_of_monotone (fun _ _ _ â†¦ Icc_subset_Icc (by gcongr) (by gcongr))
  (fun x â†¦ âŸ¨x.natAbs, by simp [le_abs, neg_le]âŸ©)

lemma Finset.tendsto_Ico_atTop_atTop : Tendsto (fun N : â„• â†¦ Ico (-N : â„¤) N) atTop atTop := by
  apply tendsto_atTop_finset_of_monotone (fun _ _ _ â†¦ Ico_subset_Ico (by omega) (by gcongr))
  exact fun x â†¦ âŸ¨x.natAbs + 1, by simpa using âŸ¨by apply le_trans _ (add_abs_nonneg x); omega,
    Int.lt_add_one_iff.mpr (le_abs_self x)âŸ© âŸ©

lemma Finset.tendsto_Ioc_atTop_atTop : Tendsto (fun N : â„• â†¦ Ioc (-N : â„¤) N) atTop atTop := by
  apply tendsto_atTop_finset_of_monotone (fun _ _ _ â†¦ Ioc_subset_Ioc (by omega) (by gcongr))
  exact fun x â†¦ âŸ¨x.natAbs + 1, by simpa using âŸ¨by apply le_trans _ (add_abs_nonneg x); omega,
    (Int.lt_add_one_iff.mpr (le_abs_self x)).leâŸ©âŸ©

lemma Finset.tendsto_Ioo_atTop_atTop : Tendsto (fun N : â„• â†¦ Ioo (-N : â„¤) N) atTop atTop := by
  apply tendsto_atTop_finset_of_monotone (fun _ _ _ â†¦ Ioo_subset_Ioo (by omega) (by gcongr))
  exact fun x â†¦ âŸ¨x.natAbs + 1, by simpa using âŸ¨by apply le_trans _ (add_abs_nonneg x); omega,
    (Int.lt_add_one_iff.mpr (le_abs_self x))âŸ©âŸ©

variable (G : Type*) [Neg G] [Preorder G] [LocallyFiniteOrder G] [(atTop : Filter G).NeBot]

/-- The SummationFilter on Locally finite order `G` corresponding to the symmetric
intervals `Icc (-N) N`Â· -/
def SummationFilter.SymmetricConditional :
    SummationFilter G where
  filter := atTop.map (fun g â†¦ Icc (-g) g)

abbrev SummationFilter.IccFilter : SummationFilter â„¤ :=
  SummationFilter.SymmetricConditional â„¤

lemma SymmetricConditional_eq_map_Icc :
    IccFilter.filter = atTop.map (fun N : â„• â†¦ Icc (-(N : â„¤)) N) := by
  rw [IccFilter, SymmetricConditional, â† Nat.map_cast_int_atTop]
  rfl

/-- The SummationFilter on `â„¤` corresponding to the intervals `Ico (-N) N`. -/
abbrev SummationFilter.IcoFilter : SummationFilter â„¤ where
  filter := atTop.map (fun N : â„• â†¦ Ico (-(N : â„¤)) N)

/-- The SummationFilter on `â„¤` corresponding to the intervals `Ioc (-N) N`. -/
abbrev SummationFilter.IocFilter : SummationFilter â„¤ where
  filter := atTop.map (fun N : â„• â†¦ Ioc (-(N : â„¤)) N)

lemma SymmetricConditional_eq_map_Ioo :
    (SymmetricConditional â„¤).filter = atTop.map (fun N : â„• â†¦ Ioo (-(N : â„¤)) N) := by
  rw [SymmetricConditional, â† Nat.map_cast_int_atTop]
  ext s
  simp only [Filter.mem_map, mem_atTop_sets, ge_iff_le, Set.mem_preimage]
  constructor
  Â· intro âŸ¨a, haâŸ©
    refine âŸ¨a + 1, fun b hb â†¦ ?_âŸ©
    convert ha (b - 1) (by grind) using 1
    ext x
    rw [mem_Ioo, mem_Icc]
    grind
  Â· intro âŸ¨a, haâŸ©
    refine âŸ¨a - 1, fun b hb â†¦ ?_âŸ©
    convert ha (b + 1) (by grind) using 1
    ext x
    simp only [mem_Icc, Nat.cast_add, Nat.cast_one, neg_add_rev, Int.reduceNeg, mem_Ioo,
      add_neg_lt_iff_lt_add]
    grind

instance : (SymmetricConditional G).NeBot where
  ne_bot := by simp [SymmetricConditional, Filter.NeBot.map]

instance : (IcoFilter).NeBot where
  ne_bot := by simp [Filter.NeBot.map]

instance : (IocFilter).NeBot where
  ne_bot := by simp [Filter.NeBot.map]

instance : IccFilter.LeAtTop where
  le_atTop := by
    rw [SymmetricConditional_eq_map_Icc, @map_le_iff_le_comap, â† @tendsto_iff_comap]
    exact tendsto_Icc_atTop_atTop

instance : (IcoFilter).LeAtTop where
  le_atTop := by
    rw [@map_le_iff_le_comap, â† @tendsto_iff_comap]
    exact tendsto_Ico_atTop_atTop

instance : (IocFilter).LeAtTop where
  le_atTop := by
    rw [@map_le_iff_le_comap, â† @tendsto_iff_comap]
    exact tendsto_Ioc_atTop_atTop

variable {Î± : Type*} {f : â„¤ â†’ Î±} [CommGroup Î±] [TopologicalSpace Î±] [ContinuousMul Î±]

@[to_additive]
lemma multipliable_IcoFilter_of_multiplible_SymmetricConditional
    (hf : Multipliable f IccFilter) (hf2 : Tendsto (fun N : â„• â†¦ (f N)â»Â¹) atTop (ð“ 1)) :
    Multipliable f IcoFilter := by
  have := (hf.hasProd)
  apply HasProd.multipliable (a := âˆ'[IccFilter] (b : â„¤), f b)
  simp only [HasProd, tendsto_map'_iff, SymmetricConditional_eq_map_Icc] at *
  apply Filter.Tendsto_of_div_tendsto_one _ this
  conv =>
    enter [1, N]
    simp only [Pi.div_apply, comp_apply]
    rw [prod_Icc_eq_prod_Ico_succ _ (by omega)]
  simpa using hf2

@[to_additive]
lemma tprod_SymmetricConditional_eq_tprod_IcoFilter [T2Space Î±]
    (hf : Multipliable f IccFilter) (hf2 : Tendsto (fun N : â„• â†¦ (f N)â»Â¹) atTop (ð“ 1)) :
    âˆ'[IccFilter] b, f b = âˆ'[IcoFilter] b, f b := by
  have := (hf.hasProd)
  apply symm
  apply HasProd.tprod_eq
  simp only [HasProd, tendsto_map'_iff, SymmetricConditional_eq_map_Icc] at *
  apply Filter.Tendsto_of_div_tendsto_one _ this
  conv =>
    enter [1, N]
    simp only [Pi.div_apply, comp_apply]
    rw [prod_Icc_eq_prod_Ico_succ _ (by omega)]
  simpa using hf2

end IntervalFilters
