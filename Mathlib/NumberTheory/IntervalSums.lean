/-
Copyright (c) 2025 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/

import Mathlib.Algebra.Order.Ring.Star
import Mathlib.Algebra.Order.Archimedean.Basic
import Mathlib.Algebra.CharP.Defs
import Mathlib.Topology.Algebra.Monoid.Defs
import Mathlib.Topology.Algebra.InfiniteSum.Defs
import Mathlib.Data.Int.Interval
import Mathlib.Order.Filter.AtTopBot.Finset
import Mathlib.Order.Filter.AtTopBot.Group
import Mathlib.Algebra.Group.EvenFunction

/-!
# Sums over symmetric integer intervals

This file contains some lemmas about sums over symmetric integer intervals `Ixx -N N` used in the
definition of the Eisenstein series `E2`. In particular we define `SymmetricConditional`,
`IcoFilter`, `IocFilter` as `SummationFilter`s  corresponding to the intervals
`Icc -N N`, `Ico -N N`, `Ioc -N N` respectively.
We also prove that these filters are all `NeBot` and `LeAtTop`.

-/

open Finset Topology Function

@[to_additive]
lemma Finset.prod_Icc_of_even_eq_range {Î± : Type*} [CommGroup Î±] {f : â„¤ â†’ Î±} (hf : f.Even) (N : â„•) :
    âˆ m âˆˆ Icc (-N : â„¤) N, f m = (âˆ m âˆˆ range (N + 1), f m) ^ 2 / f 0 := by
  induction N with
  | zero => simp [sq]
  | succ N ih =>
    rw [Nat.cast_add, Nat.cast_one, Icc_succ_succ, prod_union (by simp), prod_pair (by omega), ih,
      prod_range_succ _ (N + 1), hf, â† pow_two, div_mul_eq_mul_div, â† mul_pow, Nat.cast_succ]

@[to_additive]
lemma Finset.prod_Icc_eq_prod_Ico_succ {Î± : Type*} [CommMonoid Î±] (f : â„¤ â†’ Î±) {l u : â„¤}
    (h : l â‰¤ u) : âˆ m âˆˆ Icc l u, f m = (âˆ m âˆˆ Ico l u, f m) * f u := by
  simp [Icc_eq_cons_Ico h, mul_comm]

@[to_additive]
lemma Finset.prod_Icc_mul_endpoints {R : Type*} [CommGroup R] (f : â„¤ â†’ R) {N : â„•} (hn : 1 â‰¤ N) :
    âˆ m âˆˆ Icc (-N : â„¤) N, f m = f N * f (-N : â„¤) * âˆ m âˆˆ Icc (-(N - 1) : â„¤) (N - 1), f m := by
  induction N
  Â· grind
  Â· zify
    rw [Icc_succ_succ, prod_union (by simp)]
    grind

section IntervalFilters

open Filter Function Finset SummationFilter

variable (G : Type*) [Neg G] [Preorder G] [LocallyFiniteOrder G]

namespace SummationFilter

/-- The SummationFilter on Locally finite order `G` corresponding to the symmetric
intervals `Icc (-N) N`Â· -/
def symmetricConditional : SummationFilter G where
  filter := atTop.map (fun g â†¦ Icc (-g) g)

/-- The SummationFilter on `â„¤` corresponding to the intervals `Icc -N N`. Note that this is
the same as the limit over open intervals `Ioo -N N` (see `symmetricConditional_eq_map_Ioo`). -/
abbrev symCondInt : SummationFilter â„¤ := symmetricConditional â„¤

lemma symmetricConditional_eq_map_Icc :
    (symmetricConditional G).filter = atTop.map (fun N â†¦ Icc (-N) N) := rfl

lemma symmetricConditional_eq_map_Icc_nat :
    symCondInt.filter = atTop.map (fun N : â„• â†¦ Icc (-(N : â„¤)) N) := by
  rw [symCondInt, symmetricConditional, â† Nat.map_cast_int_atTop]
  rfl

/-- The SummationFilter on `G` corresponding to the intervals `Ico (-N) N`. -/
def IcoFilter : SummationFilter G where
  filter := atTop.map (fun N â†¦ Ico (-N) N)

/-- The SummationFilter on `G` corresponding to the intervals `Ioc (-N) N`. -/
def IocFilter : SummationFilter G where
  filter := atTop.map (fun N â†¦ Ioc (-N) N)

lemma symmetricConditional_eq_map_Ioo :
    symCondInt.filter = atTop.map (fun N â†¦ Ioo (-N) N) := by
  simp_rw [symmetricConditional, â† Nat.map_cast_int_atTop]
  ext s
  simp only [Filter.mem_map, mem_atTop_sets, ge_iff_le, Set.mem_preimage]
  constructor
  Â· intro âŸ¨a, haâŸ©
    refine âŸ¨a + 1, fun b hb â†¦ ?_âŸ©
    convert ha (b - 1) (by grind) using 1
    ext x
    rw [mem_Ioo, mem_Icc]
    grind
  Â· intro âŸ¨a, haâŸ©
    refine âŸ¨a - 1, fun b hb â†¦ ?_âŸ©
    convert ha (b + 1) (by grind) using 1
    ext x
    rw [mem_Icc, mem_Ioo]
    grind

variable [(atTop : Filter G).NeBot]

instance : (symmetricConditional G).NeBot where
  ne_bot := by simp [symmetricConditional, Filter.NeBot.map]

instance : (IcoFilter G).NeBot where
  ne_bot := by simp [IcoFilter, Filter.NeBot.map]

instance : (IocFilter G).NeBot where
  ne_bot := by simp [IocFilter, Filter.NeBot.map]

section LeAtTop

variable {G : Type*} [AddCommGroup G] [PartialOrder G] [IsOrderedAddMonoid G] [LocallyFiniteOrder G]

lemma symmetricConditional_le_Conditional :
    (symmetricConditional G).filter â‰¤ (conditional G).filter :=
  Filter.map_mono (tendsto_neg_atTop_atBot.prodMk tendsto_id)

instance : (symmetricConditional G).LeAtTop where
  le_atTop := le_trans symmetricConditional_le_Conditional (conditional G).le_atTop

instance : (IcoFilter â„¤).LeAtTop where
  le_atTop := by
    rw [IcoFilter, map_le_iff_le_comap, â† @tendsto_iff_comap]
    exact tendsto_Ico_atTop_atTop

instance : (IocFilter â„¤).LeAtTop where
  le_atTop := by
    rw [IocFilter, map_le_iff_le_comap, â† @tendsto_iff_comap]
    exact tendsto_Ioc_atTop_atTop

end LeAtTop

end SummationFilter

section comparisons

variable {Î± : Type*} {f : â„¤ â†’ Î±} [CommGroup Î±] [TopologicalSpace Î±] [ContinuousMul Î±]

@[to_additive]
lemma multipliable_IcoFilter_of_multiplible_symmetricConditional
    (hf : Multipliable f symCondInt) (hf2 : Tendsto (fun N : â„• â†¦ (f N)â»Â¹) atTop (ğ“ 1)) :
    Multipliable f (IcoFilter â„¤) := by
  have := (hf.hasProd)
  apply HasProd.multipliable (a := âˆ'[symCondInt] (b : â„¤), f b)
  simp only [HasProd, tendsto_map'_iff, symmetricConditional_eq_map_Icc_nat,
    â† Nat.map_cast_int_atTop, IcoFilter] at *
  apply tendsto_of_div_tendsto_one _ this
  conv =>
    enter [1, N]
    simp only [Pi.div_apply, comp_apply]
    rw [prod_Icc_eq_prod_Ico_succ _ (by omega)]
  simpa using hf2

@[to_additive]
lemma tprod_symmetricConditional_eq_tprod_IcoFilter [T2Space Î±]
    (hf : Multipliable f symCondInt) (hf2 : Tendsto (fun N : â„• â†¦ (f N)â»Â¹) atTop (ğ“ 1)) :
    âˆ'[symCondInt] b, f b = âˆ'[IcoFilter â„¤] b, f b := by
  have := (hf.hasProd)
  apply symm
  apply HasProd.tprod_eq
  simp only [HasProd, tendsto_map'_iff, symmetricConditional_eq_map_Icc_nat,
    â† Nat.map_cast_int_atTop, IcoFilter] at *
  apply tendsto_of_div_tendsto_one _ this
  conv =>
    enter [1, N]
    simp only [Pi.div_apply, comp_apply]
    rw [prod_Icc_eq_prod_Ico_succ _ (by omega)]
  simpa using hf2

@[to_additive]
lemma HasProd_symCondInt_iff {Î± : Type*} [CommMonoid Î±] [TopologicalSpace Î±]
    {f : â„¤ â†’ Î±} {a : Î±} : HasProd f a symCondInt â†”
    Tendsto (fun N : â„• â†¦ âˆ n âˆˆ Finset.Icc (-(N : â„¤)) (N : â„¤), f n) atTop (ğ“ a) := by
  simp only [HasProd, symCondInt, symmetricConditional_eq_map_Icc_nat, tendsto_map'_iff]
  rfl

@[to_additive]
lemma HasProd_IcoFilter_iff {Î± : Type*} [CommMonoid Î±] [TopologicalSpace Î±]
    {f : â„¤ â†’ Î±} {a : Î±} : HasProd f a (IcoFilter â„¤) â†”
    Tendsto (fun N : â„• â†¦ âˆ n âˆˆ Finset.Ico (-(N : â„¤)) (N : â„¤), f n) atTop (ğ“ a) := by
  simp only [HasProd, IcoFilter, â† Nat.map_cast_int_atTop, Filter.map_map, tendsto_map'_iff]
  rfl

@[to_additive]
lemma HasProd_IocFilter_iff {Î± : Type*} [CommMonoid Î±] [TopologicalSpace Î±]
    {f : â„¤ â†’ Î±} {a : Î±} : HasProd f a (IocFilter â„¤) â†”
    Tendsto (fun N : â„• â†¦ âˆ n âˆˆ Finset.Ioc (-(N : â„¤)) (N : â„¤), f n) atTop (ğ“ a) := by
  simp only [HasProd, IocFilter, â† Nat.map_cast_int_atTop, Filter.map_map, tendsto_map'_iff]
  rfl

end comparisons

end IntervalFilters
