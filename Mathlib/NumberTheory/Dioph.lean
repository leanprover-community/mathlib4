/-
Copyright (c) 2017 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro
-/
import Mathlib.Data.Fin.Fin2
import Mathlib.Data.PFun
import Mathlib.Data.Vector3
import Mathlib.NumberTheory.PellMatiyasevic

#align_import number_theory.dioph from "leanprover-community/mathlib"@"a66d07e27d5b5b8ac1147cacfe353478e5c14002"

/-!
# Diophantine functions and Matiyasevic's theorem

Hilbert's tenth problem asked whether there exists an algorithm which for a given integer polynomial
determines whether this polynomial has integer solutions. It was answered in the negative in 1970,
the final step being completed by Matiyasevic who showed that the power function is Diophantine.

Here a function is called Diophantine if its graph is Diophantine as a set. A subset `S ‚äÜ ‚Ñï ^ Œ±` in
turn is called Diophantine if there exists an integer polynomial on `Œ± ‚äï Œ≤` such that `v ‚àà S` iff
there exists `t : ‚Ñï^Œ≤` with `p (v, t) = 0`.

## Main definitions

* `IsPoly`: a predicate stating that a function is a multivariate integer polynomial.
* `Poly`: the type of multivariate integer polynomial functions.
* `Dioph`: a predicate stating that a set is Diophantine, i.e. a set `S ‚äÜ ‚Ñï^Œ±` is
  Diophantine if there exists a polynomial on `Œ± ‚äï Œ≤` such that `v ‚àà S` iff there
  exists `t : ‚Ñï^Œ≤` with `p (v, t) = 0`.
* `dioph_fn`: a predicate on a function stating that it is Diophantine in the sense that its graph
  is Diophantine as a set.

## Main statements

* `pell_dioph` states that solutions to Pell's equation form a Diophantine set.
* `pow_dioph` states that the power function is Diophantine, a version of Matiyasevic's theorem.

## References

* [M. Carneiro, _A Lean formalization of Matiyasevic's theorem_][carneiro2018matiyasevic]
* [M. Davis, _Hilbert's tenth problem is unsolvable_][MR317916]

## Tags

Matiyasevic's theorem, Hilbert's tenth problem

## TODO

* Finish the solution of Hilbert's tenth problem.
* Connect `Poly` to `MvPolynomial`
-/


open Fin2 Function Nat Sum

local infixr:67 " ::‚Çí " => Option.elim'

local infixr:65 " ‚äó " => Sum.elim

universe u

/-!
### Multivariate integer polynomials

Note that this duplicates `MvPolynomial`.
-/


section Polynomials

variable {Œ± Œ≤ Œ≥ : Type*}

/-- A predicate asserting that a function is a multivariate integer polynomial.
  (We are being a bit lazy here by allowing many representations for multiplication,
  rather than only allowing monomials and addition, but the definition is equivalent
  and this is easier to use.) -/
inductive IsPoly : ((Œ± ‚Üí ‚Ñï) ‚Üí ‚Ñ§) ‚Üí Prop
  | proj : ‚àÄ i, IsPoly fun x : Œ± ‚Üí ‚Ñï => x i
  | const : ‚àÄ n : ‚Ñ§, IsPoly fun _ : Œ± ‚Üí ‚Ñï => n
  | sub : ‚àÄ {f g : (Œ± ‚Üí ‚Ñï) ‚Üí ‚Ñ§}, IsPoly f ‚Üí IsPoly g ‚Üí IsPoly fun x => f x - g x
  | mul : ‚àÄ {f g : (Œ± ‚Üí ‚Ñï) ‚Üí ‚Ñ§}, IsPoly f ‚Üí IsPoly g ‚Üí IsPoly fun x => f x * g x
#align is_poly IsPoly

theorem IsPoly.neg {f : (Œ± ‚Üí ‚Ñï) ‚Üí ‚Ñ§} : IsPoly f ‚Üí IsPoly (-f) := by
  rw [‚Üê zero_sub]; exact (IsPoly.const 0).sub
  -- ‚ä¢ IsPoly f ‚Üí IsPoly (0 - f)
                   -- üéâ no goals
#align is_poly.neg IsPoly.neg

theorem IsPoly.add {f g : (Œ± ‚Üí ‚Ñï) ‚Üí ‚Ñ§} (hf : IsPoly f) (hg : IsPoly g) : IsPoly (f + g) := by
  rw [‚Üê sub_neg_eq_add]; exact hf.sub hg.neg
  -- ‚ä¢ IsPoly (f - -g)
                         -- üéâ no goals
#align is_poly.add IsPoly.add

/-- The type of multivariate integer polynomials -/
def Poly (Œ± : Type u) := { f : (Œ± ‚Üí ‚Ñï) ‚Üí ‚Ñ§ // IsPoly f }
#align poly Poly

namespace Poly

section

instance funLike : FunLike (Poly Œ±) (Œ± ‚Üí ‚Ñï) fun _ => ‚Ñ§ :=
  ‚ü®Subtype.val, Subtype.val_injective‚ü©
#align poly.fun_like Poly.funLike

-- Porting note: This instance is not necessary anymore
-- /-- Helper instance for when there are too many metavariables to apply `fun_like.has_coe_to_fun`
-- directly. -/
-- instance : CoeFun (Poly Œ±) fun _ => (Œ± ‚Üí ‚Ñï) ‚Üí ‚Ñ§ := FunLike.hasCoeToFun

/-- The underlying function of a `Poly` is a polynomial -/
protected theorem isPoly (f : Poly Œ±) : IsPoly f := f.2
#align poly.is_poly Poly.isPoly

/-- Extensionality for `Poly Œ±` -/
@[ext]
theorem ext {f g : Poly Œ±} : (‚àÄ x, f x = g x) ‚Üí f = g := FunLike.ext _ _
#align poly.ext Poly.ext

/-- The `i`th projection function, `x_i`. -/
def proj (i : Œ±) : Poly Œ± := ‚ü®_, IsPoly.proj i‚ü©
#align poly.proj Poly.proj

@[simp]
theorem proj_apply (i : Œ±) (x) : proj i x = x i := rfl
#align poly.proj_apply Poly.proj_apply

/-- The constant function with value `n : ‚Ñ§`. -/
def const (n : ‚Ñ§) : Poly Œ± := ‚ü®_, IsPoly.const n‚ü©
#align poly.const Poly.const

@[simp]
theorem const_apply (n) (x : Œ± ‚Üí ‚Ñï) : const n x = n := rfl
#align poly.const_apply Poly.const_apply

instance : Zero (Poly Œ±) := ‚ü®const 0‚ü©

instance : One (Poly Œ±) := ‚ü®const 1‚ü©

instance : Neg (Poly Œ±) := ‚ü®fun f => ‚ü®-f, f.2.neg‚ü©‚ü©

instance : Add (Poly Œ±) := ‚ü®fun f g => ‚ü®f + g, f.2.add g.2‚ü©‚ü©

instance : Sub (Poly Œ±) := ‚ü®fun f g => ‚ü®f - g, f.2.sub g.2‚ü©‚ü©

instance : Mul (Poly Œ±) := ‚ü®fun f g => ‚ü®f * g, f.2.mul g.2‚ü©‚ü©

@[simp]
theorem coe_zero : ‚áë(0 : Poly Œ±) = const 0 := rfl
#align poly.coe_zero Poly.coe_zero

@[simp]
theorem coe_one : ‚áë(1 : Poly Œ±) = const 1 := rfl
#align poly.coe_one Poly.coe_one

@[simp]
theorem coe_neg (f : Poly Œ±) : ‚áë(-f) = -f := rfl
#align poly.coe_neg Poly.coe_neg

@[simp]
theorem coe_add (f g : Poly Œ±) : ‚áë(f + g) = f + g := rfl
#align poly.coe_add Poly.coe_add

@[simp]
theorem coe_sub (f g : Poly Œ±) : ‚áë(f - g) = f - g := rfl
#align poly.coe_sub Poly.coe_sub

@[simp]
theorem coe_mul (f g : Poly Œ±) : ‚áë(f * g) = f * g := rfl
#align poly.coe_mul Poly.coe_mul

@[simp]
theorem zero_apply (x) : (0 : Poly Œ±) x = 0 := rfl
#align poly.zero_apply Poly.zero_apply

@[simp]
theorem one_apply (x) : (1 : Poly Œ±) x = 1 := rfl
#align poly.one_apply Poly.one_apply

@[simp]
theorem neg_apply (f : Poly Œ±) (x) : (-f) x = -f x := rfl
#align poly.neg_apply Poly.neg_apply

@[simp]
theorem add_apply (f g : Poly Œ±) (x : Œ± ‚Üí ‚Ñï) : (f + g) x = f x + g x := rfl
#align poly.add_apply Poly.add_apply

@[simp]
theorem sub_apply (f g : Poly Œ±) (x : Œ± ‚Üí ‚Ñï) : (f - g) x = f x - g x := rfl
#align poly.sub_apply Poly.sub_apply

@[simp]
theorem mul_apply (f g : Poly Œ±) (x : Œ± ‚Üí ‚Ñï) : (f * g) x = f x * g x := rfl
#align poly.mul_apply Poly.mul_apply

instance (Œ± : Type*) : Inhabited (Poly Œ±) := ‚ü®0‚ü©

instance : AddCommGroup (Poly Œ±) := by
  refine' { add := ((¬∑ + ¬∑) : Poly Œ± ‚Üí Poly Œ± ‚Üí Poly Œ±)
            neg := (Neg.neg : Poly Œ± ‚Üí Poly Œ±)
            sub := Sub.sub
            zero := 0
            zsmul := @zsmulRec _ ‚ü®(0 : Poly Œ±)‚ü© ‚ü®(¬∑ + ¬∑)‚ü© ‚ü®Neg.neg‚ü©
            nsmul := @nsmulRec _ ‚ü®(0 : Poly Œ±)‚ü© ‚ü®(¬∑ + ¬∑)‚ü©
            .. }
  all_goals
    intros
    first
      | rfl
      | ext; simp_rw [add_apply]
        first
          | rw [‚Üê add_assoc]
          | rw [zero_apply, zero_add]
          | rw [zero_apply, add_zero]
          | rw [neg_apply, add_left_neg, zero_apply]
          | rw [add_comm]

instance : AddGroupWithOne (Poly Œ±) :=
  { (inferInstance : AddCommGroup (Poly Œ±)) with
      one := 1
      natCast := fun n => Poly.const n
      intCast := Poly.const }

instance : CommRing (Poly Œ±) := by
  refine' { (inferInstance : AddCommGroup (Poly Œ±)),
            (inferInstance : AddGroupWithOne (Poly Œ±)) with
              add := ((¬∑ + ¬∑) : Poly Œ± ‚Üí Poly Œ± ‚Üí Poly Œ±)
              zero := 0
              mul := (¬∑ * ¬∑)
              one := 1
              npow := @npowRec _ ‚ü®(1 : Poly Œ±)‚ü© ‚ü®(¬∑ * ¬∑)‚ü©
              .. }
  all_goals
    intros
    first
      | rfl
      | ext; simp only [add_apply, mul_apply]
        first
          | rw [mul_add]
          | rw [add_mul]
          | rw [zero_apply, zero_mul]
          | rw [zero_apply, mul_zero]
          | rw [‚Üê mul_assoc]
          | rw [one_apply, one_mul]
          | rw [one_apply, mul_one]
          | rw [mul_comm]

theorem induction {C : Poly Œ± ‚Üí Prop} (H1 : ‚àÄ i, C (proj i)) (H2 : ‚àÄ n, C (const n))
    (H3 : ‚àÄ f g, C f ‚Üí C g ‚Üí C (f - g)) (H4 : ‚àÄ f g, C f ‚Üí C g ‚Üí C (f * g)) (f : Poly Œ±) : C f := by
  cases' f with f pf
  -- ‚ä¢ C { val := f, property := pf }
  induction' pf with i n f g pf pg ihf ihg f g pf pg ihf ihg
  apply H1; apply H2; apply H3 _ _ ihf ihg; apply H4 _ _ ihf ihg
            -- ‚ä¢ C { val := fun x => f x - g x, property := (_ : IsPoly fun x => f x - g x) }
                      -- ‚ä¢ C { val := fun x => f x * g x, property := (_ : IsPoly fun x => f x * g x) }
                                            -- üéâ no goals
#align poly.induction Poly.induction

/-- The sum of squares of a list of polynomials. This is relevant for
  Diophantine equations, because it means that a list of equations
  can be encoded as a single equation: `x = 0 ‚àß y = 0 ‚àß z = 0` is
  equivalent to `x^2 + y^2 + z^2 = 0`. -/
def sumsq : List (Poly Œ±) ‚Üí Poly Œ±
  | [] => 0
  | p::ps => p * p + sumsq ps
#align poly.sumsq Poly.sumsq

theorem sumsq_nonneg (x : Œ± ‚Üí ‚Ñï) : ‚àÄ l, 0 ‚â§ sumsq l x
  | [] => le_refl 0
  | p::ps => by
    rw [sumsq]
    -- ‚ä¢ 0 ‚â§ ‚Üë(p * p + sumsq ps) x
    exact add_nonneg (mul_self_nonneg _) (sumsq_nonneg _ ps)
    -- üéâ no goals
#align poly.sumsq_nonneg Poly.sumsq_nonneg

theorem sumsq_eq_zero (x) : ‚àÄ l, sumsq l x = 0 ‚Üî l.All‚ÇÇ fun a : Poly Œ± => a x = 0
  | [] => eq_self_iff_true _
  | p::ps => by
    rw [List.all‚ÇÇ_cons, ‚Üê sumsq_eq_zero _ ps]; rw [sumsq]
    -- ‚ä¢ ‚Üë(sumsq (p :: ps)) x = 0 ‚Üî ‚Üëp x = 0 ‚àß ‚Üë(sumsq ps) x = 0
                                               -- ‚ä¢ ‚Üë(p * p + sumsq ps) x = 0 ‚Üî ‚Üëp x = 0 ‚àß ‚Üë(sumsq ps) x = 0
    exact
      ‚ü®fun h : p x * p x + sumsq ps x = 0 =>
        have : p x = 0 :=
          eq_zero_of_mul_self_eq_zero <|
            le_antisymm
              (by
                rw [‚Üê h]
                have t := add_le_add_left (sumsq_nonneg x ps) (p x * p x)
                rwa [add_zero] at t)
              (mul_self_nonneg _)
        ‚ü®this, by simp [this] at h; exact h‚ü©,
      fun ‚ü®h1, h2‚ü© => by rw [add_apply, mul_apply, h1, h2]; rfl‚ü©
#align poly.sumsq_eq_zero Poly.sumsq_eq_zero

end

/-- Map the index set of variables, replacing `x_i` with `x_(f i)`. -/
def map {Œ± Œ≤} (f : Œ± ‚Üí Œ≤) (g : Poly Œ±) : Poly Œ≤ :=
  ‚ü®fun v => g <| v ‚àò f, Poly.induction (C := fun g => IsPoly (fun v => g (v ‚àò f)))
    (fun i => by simp; apply IsPoly.proj) (fun n => by simp; apply IsPoly.const)
                 -- ‚ä¢ IsPoly fun v => ‚Üë(v (f i))
                       -- üéâ no goals
                                                       -- ‚ä¢ IsPoly fun v => n
                                                             -- üéâ no goals
    (fun f g pf pg => by simp; apply IsPoly.sub pf pg)
                         -- ‚ä¢ IsPoly fun v => ‚Üëf (v ‚àò f‚úù) - ‚Üëg (v ‚àò f‚úù)
                               -- üéâ no goals
    (fun f g pf pg => by simp; apply IsPoly.mul pf pg) _‚ü©
                         -- ‚ä¢ IsPoly fun v => ‚Üëf (v ‚àò f‚úù) * ‚Üëg (v ‚àò f‚úù)
                               -- üéâ no goals
#align poly.map Poly.map

@[simp]
theorem map_apply {Œ± Œ≤} (f : Œ± ‚Üí Œ≤) (g : Poly Œ±) (v) : map f g v = g (v ‚àò f) := rfl
#align poly.map_apply Poly.map_apply

end Poly

end Polynomials

/-! ### Diophantine sets -/


/-- A set `S ‚äÜ ‚Ñï^Œ±` is Diophantine if there exists a polynomial on
  `Œ± ‚äï Œ≤` such that `v ‚àà S` iff there exists `t : ‚Ñï^Œ≤` with `p (v, t) = 0`. -/
def Dioph {Œ± : Type u} (S : Set (Œ± ‚Üí ‚Ñï)) : Prop :=
  ‚àÉ (Œ≤ : Type u) (p : Poly (Sum Œ± Œ≤)), ‚àÄ v, S v ‚Üî ‚àÉ t, p (v ‚äó t) = 0
#align dioph Dioph

namespace Dioph

section

variable {Œ± Œ≤ Œ≥ : Type u} {S S' : Set (Œ± ‚Üí ‚Ñï)}

theorem ext (d : Dioph S) (H : ‚àÄ v, v ‚àà S ‚Üî v ‚àà S') : Dioph S' := by rwa [‚Üê Set.ext H]
                                                                     -- üéâ no goals
#align dioph.ext Dioph.ext

theorem of_no_dummies (S : Set (Œ± ‚Üí ‚Ñï)) (p : Poly Œ±) (h : ‚àÄ v, S v ‚Üî p v = 0) : Dioph S :=
  ‚ü®PEmpty, ‚ü®p.map inl, fun v => (h v).trans ‚ü®fun h => ‚ü®PEmpty.elim, h‚ü©, fun ‚ü®_, ht‚ü© => ht‚ü©‚ü©‚ü©
#align dioph.of_no_dummies Dioph.of_no_dummies

theorem inject_dummies_lem (f : Œ≤ ‚Üí Œ≥) (g : Œ≥ ‚Üí Option Œ≤) (inv : ‚àÄ x, g (f x) = some x)
    (p : Poly (Sum Œ± Œ≤)) (v : Œ± ‚Üí ‚Ñï) :
    (‚àÉ t, p (v ‚äó t) = 0) ‚Üî ‚àÉ t, p.map (inl ‚äó inr ‚àò f) (v ‚äó t) = 0 := by
  dsimp; refine ‚ü®fun t => ?_, fun t => ?_‚ü© <;> cases' t with t ht
  -- ‚ä¢ (‚àÉ t, ‚Üëp (v ‚äó t) = 0) ‚Üî ‚àÉ t, ‚Üëp ((v ‚äó t) ‚àò (inl ‚äó inr ‚àò f)) = 0
         -- ‚ä¢ ‚àÉ t, ‚Üëp ((v ‚äó t) ‚àò (inl ‚äó inr ‚àò f)) = 0
                                               -- ‚ä¢ ‚àÉ t, ‚Üëp ((v ‚äó t) ‚àò (inl ‚äó inr ‚àò f)) = 0
                                               -- ‚ä¢ ‚àÉ t, ‚Üëp (v ‚äó t) = 0
  ¬∑ have : (v ‚äó (0 ::‚Çí t) ‚àò g) ‚àò (inl ‚äó inr ‚àò f) = v ‚äó t :=
      funext fun s => by cases' s with a b <;> dsimp [(¬∑ ‚àò ¬∑)]; try rw [inv]; rfl
    exact ‚ü®(0 ::‚Çí t) ‚àò g, by rwa [this]‚ü©
    -- üéâ no goals
  ¬∑ have : v ‚äó t ‚àò f = (v ‚äó t) ‚àò (inl ‚äó inr ‚àò f) := funext fun s => by cases' s with a b <;> rfl
    -- ‚ä¢ ‚àÉ t, ‚Üëp (v ‚äó t) = 0
    exact ‚ü®t ‚àò f, by rwa [this]‚ü©
    -- üéâ no goals
#align dioph.inject_dummies_lem Dioph.inject_dummies_lem

theorem inject_dummies (f : Œ≤ ‚Üí Œ≥) (g : Œ≥ ‚Üí Option Œ≤) (inv : ‚àÄ x, g (f x) = some x)
    (p : Poly (Sum Œ± Œ≤)) (h : ‚àÄ v, S v ‚Üî ‚àÉ t, p (v ‚äó t) = 0) :
    ‚àÉ q : Poly (Sum Œ± Œ≥), ‚àÄ v, S v ‚Üî ‚àÉ t, q (v ‚äó t) = 0 :=
  ‚ü®p.map (inl ‚äó inr ‚àò f), fun v => (h v).trans <| inject_dummies_lem f g inv _ _‚ü©
#align dioph.inject_dummies Dioph.inject_dummies

variable (Œ≤)

theorem reindex_dioph (f : Œ± ‚Üí Œ≤) : ‚àÄ _ : Dioph S, Dioph {v | v ‚àò f ‚àà S}
  | ‚ü®Œ≥, p, pe‚ü© => ‚ü®Œ≥, p.map (inl ‚àò f ‚äó inr), fun v =>
      (pe _).trans <|
        exists_congr fun t =>
          suffices v ‚àò f ‚äó t = (v ‚äó t) ‚àò (inl ‚àò f ‚äó inr) by simp [this]
                                                            -- üéâ no goals
                             -- ‚ä¢ (v ‚àò f ‚äó t) (inl a) = ((v ‚äó t) ‚àò (inl ‚àò f ‚äó inr)) (inl a)
                                                   -- üéâ no goals
                                                   -- üéâ no goals
          funext fun s => by cases' s with a b <;> rfl‚ü©
#align dioph.reindex_dioph Dioph.reindex_dioph

variable {Œ≤}

theorem DiophList.all‚ÇÇ (l : List (Set <| Œ± ‚Üí ‚Ñï)) (d : l.All‚ÇÇ Dioph) :
    Dioph {v | l.All‚ÇÇ fun S : Set (Œ± ‚Üí ‚Ñï) => v ‚àà S} := by
  suffices ‚àÉ (Œ≤ : _) (pl : List (Poly (Sum Œ± Œ≤))), ‚àÄ v, List.All‚ÇÇ (fun S : Set _ => S v) l ‚Üî
          ‚àÉ t, List.All‚ÇÇ (fun p : Poly (Sum Œ± Œ≤) => p (v ‚äó t) = 0) pl
    from
    let ‚ü®Œ≤, pl, h‚ü© := this
    ‚ü®Œ≤, Poly.sumsq pl, fun v => (h v).trans <| exists_congr fun t => (Poly.sumsq_eq_zero _ _).symm‚ü©
  induction' l with S l IH
  -- ‚ä¢ ‚àÉ Œ≤ pl, ‚àÄ (v : Œ± ‚Üí ‚Ñï), List.All‚ÇÇ (fun S => S v) [] ‚Üî ‚àÉ t, List.All‚ÇÇ (fun p = ‚Ä¶
  exact ‚ü®ULift Empty, [], fun _ => by simp‚ü©
  -- ‚ä¢ ‚àÉ Œ≤ pl, ‚àÄ (v : Œ± ‚Üí ‚Ñï), List.All‚ÇÇ (fun S => S v) (S :: l) ‚Üî ‚àÉ t, List.All‚ÇÇ (f ‚Ä¶
  simp at d
  -- ‚ä¢ ‚àÉ Œ≤ pl, ‚àÄ (v : Œ± ‚Üí ‚Ñï), List.All‚ÇÇ (fun S => S v) (S :: l) ‚Üî ‚àÉ t, List.All‚ÇÇ (f ‚Ä¶
  exact
    let ‚ü®‚ü®Œ≤, p, pe‚ü©, dl‚ü© := d
    let ‚ü®Œ≥, pl, ple‚ü© := IH dl
    ‚ü®Sum Œ≤ Œ≥, p.map (inl ‚äó inr ‚àò inl)::pl.map fun q => q.map (inl ‚äó inr ‚àò inr),
      fun v => by
      simp; exact
        Iff.trans (and_congr (pe v) (ple v))
          ‚ü®fun ‚ü®‚ü®m, hm‚ü©, ‚ü®n, hn‚ü©‚ü© =>
            ‚ü®m ‚äó n, by
              rw [show (v ‚äó m ‚äó n) ‚àò (inl ‚äó inr ‚àò inl) = v ‚äó m from
                    funext fun s => by cases' s with a b <;> rfl]; exact hm, by
              refine List.All‚ÇÇ.imp (fun q hq => ?_) hn; dsimp [(¬∑ ‚àò ¬∑)]
              rw [show
                    (fun x : Sum Œ± Œ≥ => (v ‚äó m ‚äó n) ((inl ‚äó fun x : Œ≥ => inr (inr x)) x)) = v ‚äó n
                    from funext fun s => by cases' s with a b <;> rfl]; exact hq‚ü©,
            fun ‚ü®t, hl, hr‚ü© =>
            ‚ü®‚ü®t ‚àò inl, by
                rwa [show (v ‚äó t) ‚àò (inl ‚äó inr ‚àò inl) = v ‚äó t ‚àò inl from
                    funext fun s => by cases' s with a b <;> rfl] at hl‚ü©,
              ‚ü®t ‚àò inr, by
                refine List.All‚ÇÇ.imp (fun q hq => ?_) hr; dsimp [(¬∑ ‚àò ¬∑)] at hq
                rwa [show
                    (fun x : Sum Œ± Œ≥ => (v ‚äó t) ((inl ‚äó fun x : Œ≥ => inr (inr x)) x)) =
                      v ‚äó t ‚àò inr
                    from funext fun s => by cases' s with a b <;> rfl] at hq ‚ü©‚ü©‚ü©‚ü©
#align dioph.dioph_list.all‚ÇÇ Dioph.DiophList.all‚ÇÇ

theorem inter (d : Dioph S) (d' : Dioph S') : Dioph (S ‚à© S') := DiophList.all‚ÇÇ [S, S'] ‚ü®d, d'‚ü©
#align dioph.inter Dioph.inter

theorem union : ‚àÄ (_ : Dioph S) (_ : Dioph S'), Dioph (S ‚à™ S')
  | ‚ü®Œ≤, p, pe‚ü©, ‚ü®Œ≥, q, qe‚ü© =>
    ‚ü®Sum Œ≤ Œ≥, p.map (inl ‚äó inr ‚àò inl) * q.map (inl ‚äó inr ‚àò inr), fun v => by
      refine
        Iff.trans (or_congr ((pe v).trans ?_) ((qe v).trans ?_))
          (exists_or.symm.trans
            (exists_congr fun t =>
              (@mul_eq_zero _ _ _ (p ((v ‚äó t) ‚àò (inl ‚äó inr ‚àò inl)))
                  (q ((v ‚äó t) ‚àò (inl ‚äó inr ‚àò inr)))).symm))
      -- Porting note: putting everything on the same line fails
      ¬∑ refine inject_dummies_lem _ ?_ ?_ _ _
        -- ‚ä¢ Œ≤ ‚äï Œ≥ ‚Üí Option Œ≤
        exact some ‚äó fun _ => none
        -- ‚ä¢ ‚àÄ (x : Œ≤), (some ‚äó fun x => none) (inl x) = some x
        exact fun _ => by simp only [elim_inl]
        -- üéâ no goals
      -- Porting note: putting everything on the same line fails
      ¬∑ refine inject_dummies_lem _ ?_ ?_ _ _
        -- ‚ä¢ Œ≤ ‚äï Œ≥ ‚Üí Option Œ≥
        exact (fun _ => none) ‚äó some
        -- ‚ä¢ ‚àÄ (x : Œ≥), ((fun x => none) ‚äó some) (inr x) = some x
        exact fun _ => by simp only [elim_inr]‚ü©
        -- üéâ no goals
#align dioph.union Dioph.union

/-- A partial function is Diophantine if its graph is Diophantine. -/
def DiophPfun (f : (Œ± ‚Üí ‚Ñï) ‚Üí. ‚Ñï) : Prop :=
  Dioph {v : Option Œ± ‚Üí ‚Ñï | f.graph (v ‚àò some, v none)}
#align dioph.dioph_pfun Dioph.DiophPfun

/-- A function is Diophantine if its graph is Diophantine. -/
def DiophFn (f : (Œ± ‚Üí ‚Ñï) ‚Üí ‚Ñï) : Prop :=
  Dioph {v : Option Œ± ‚Üí ‚Ñï | f (v ‚àò some) = v none}
#align dioph.dioph_fn Dioph.DiophFn

theorem reindex_diophFn {f : (Œ± ‚Üí ‚Ñï) ‚Üí ‚Ñï} (g : Œ± ‚Üí Œ≤) (d : DiophFn f) :
    DiophFn fun v => f (v ‚àò g) := by convert reindex_dioph (Option Œ≤) (Option.map g) d
                                     -- üéâ no goals
#align dioph.reindex_dioph_fn Dioph.reindex_diophFn

theorem ex_dioph {S : Set (Sum Œ± Œ≤ ‚Üí ‚Ñï)} : Dioph S ‚Üí Dioph {v | ‚àÉ x, v ‚äó x ‚àà S}
  | ‚ü®Œ≥, p, pe‚ü© =>
    ‚ü®Sum Œ≤ Œ≥, p.map ((inl ‚äó inr ‚àò inl) ‚äó inr ‚àò inr), fun v =>
      ‚ü®fun ‚ü®x, hx‚ü© =>
        let ‚ü®t, ht‚ü© := (pe _).1 hx
        ‚ü®x ‚äó t, by
          simp; rw [show (v ‚äó x ‚äó t) ‚àò ((inl ‚äó inr ‚àò inl) ‚äó inr ‚àò inr) = (v ‚äó x) ‚äó t from
          -- ‚ä¢ ‚Üëp ((v ‚äó x ‚äó t) ‚àò ((inl ‚äó inr ‚àò inl) ‚äó inr ‚àò inr)) = 0
            funext fun s => by cases' s with a b <;> try { cases a <;> rfl }; rfl]
          exact ht‚ü©,
          -- üéâ no goals
        fun ‚ü®t, ht‚ü© =>
        ‚ü®t ‚àò inl,
          (pe _).2
            ‚ü®t ‚àò inr, by
              simp at ht
              -- ‚ä¢ ‚Üëp ((v ‚äó t ‚àò inl) ‚äó t ‚àò inr) = 0
              rwa [show (v ‚äó t) ‚àò ((inl ‚äó inr ‚àò inl) ‚äó inr ‚àò inr) = (v ‚äó t ‚àò inl) ‚äó t ‚àò inr from
                funext fun s => by cases' s with a b <;> try { cases a <;> rfl }; rfl] at ht‚ü©‚ü©‚ü©‚ü©
#align dioph.ex_dioph Dioph.ex_dioph

theorem ex1_dioph {S : Set (Option Œ± ‚Üí ‚Ñï)} : Dioph S ‚Üí Dioph {v | ‚àÉ x, x ::‚Çí v ‚àà S}
  | ‚ü®Œ≤, p, pe‚ü© =>
    ‚ü®Option Œ≤, p.map (inr none ::‚Çí inl ‚äó inr ‚àò some), fun v =>
      ‚ü®fun ‚ü®x, hx‚ü© =>
        let ‚ü®t, ht‚ü© := (pe _).1 hx
        ‚ü®x ::‚Çí t, by
          simp
          -- ‚ä¢ ‚Üëp ((v ‚äó x ::‚Çí t) ‚àò (inr none ::‚Çí inl ‚äó inr ‚àò some)) = 0
          rw [show (v ‚äó x ::‚Çí t) ‚àò (inr none ::‚Çí inl ‚äó inr ‚àò some) = x ::‚Çí v ‚äó t from
            funext fun s => by cases' s with a b <;> try { cases a <;> rfl}; rfl]
          exact ht‚ü©,
          -- üéâ no goals
        fun ‚ü®t, ht‚ü© =>
        ‚ü®t none,
          (pe _).2
            ‚ü®t ‚àò some, by
              simp at ht
              -- ‚ä¢ ‚Üëp (t none ::‚Çí v ‚äó t ‚àò some) = 0
              rwa [show (v ‚äó t) ‚àò (inr none ::‚Çí inl ‚äó inr ‚àò some) = t none ::‚Çí v ‚äó t ‚àò some from
                funext fun s => by cases' s with a b <;> try { cases a <;> rfl }; rfl] at ht ‚ü©‚ü©‚ü©‚ü©
#align dioph.ex1_dioph Dioph.ex1_dioph

theorem dom_dioph {f : (Œ± ‚Üí ‚Ñï) ‚Üí. ‚Ñï} (d : DiophPfun f) : Dioph f.Dom :=
  cast (congr_arg Dioph <| Set.ext fun _ => (PFun.dom_iff_graph _ _).symm) (ex1_dioph d)
#align dioph.dom_dioph Dioph.dom_dioph

theorem diophFn_iff_pFun (f : (Œ± ‚Üí ‚Ñï) ‚Üí ‚Ñï) : DiophFn f = @DiophPfun Œ± f := by
  refine' congr_arg Dioph (Set.ext fun v => _); exact PFun.lift_graph.symm
  -- ‚ä¢ v ‚àà {v | f (v ‚àò some) = v none} ‚Üî v ‚àà {v | PFun.graph ‚Üëf (v ‚àò some, v none)}
                                                -- üéâ no goals
#align dioph.dioph_fn_iff_pfun Dioph.diophFn_iff_pFun

theorem abs_poly_dioph (p : Poly Œ±) : DiophFn fun v => (p v).natAbs :=
  of_no_dummies _ ((p.map some - Poly.proj none) * (p.map some + Poly.proj none))
    fun v => (by dsimp; exact Int.eq_natAbs_iff_mul_eq_zero)
                 -- ‚ä¢ setOf (fun v => Int.natAbs (‚Üëp (v ‚àò some)) = v none) v ‚Üî (‚Üëp (v ‚àò some) - ‚Üë( ‚Ä¶
                        -- üéâ no goals
#align dioph.abs_poly_dioph Dioph.abs_poly_dioph

theorem proj_dioph (i : Œ±) : DiophFn fun v => v i :=
  abs_poly_dioph (Poly.proj i)
#align dioph.proj_dioph Dioph.proj_dioph

theorem diophPfun_comp1 {S : Set (Option Œ± ‚Üí ‚Ñï)} (d : Dioph S) {f} (df : DiophPfun f) :
    Dioph {v : Œ± ‚Üí ‚Ñï | ‚àÉ h : f.Dom v, f.fn v h ::‚Çí v ‚àà S} :=
  ext (ex1_dioph (d.inter df)) fun v =>
    ‚ü®fun ‚ü®x, hS, (h : Exists _)‚ü© => by
      rw [show (x ::‚Çí v) ‚àò some = v from funext fun s => rfl] at h;
      -- ‚ä¢ v ‚àà {v | ‚àÉ h, PFun.fn f v h ::‚Çí v ‚àà S}
        cases' h with hf h; refine' ‚ü®hf, _‚ü©; rw [PFun.fn, h]; exact hS,
        -- ‚ä¢ v ‚àà {v | ‚àÉ h, PFun.fn f v h ::‚Çí v ‚àà S}
                            -- ‚ä¢ PFun.fn f v hf ::‚Çí v ‚àà S
                                             -- ‚ä¢ (v, (x ::‚Çí v) none).snd ::‚Çí v ‚àà S
                                                              -- üéâ no goals
    fun ‚ü®x, hS‚ü© =>
      ‚ü®f.fn v x, hS, show Exists _ by
        rw [show (f.fn v x ::‚Çí v) ‚àò some = v from funext fun s => rfl]; exact ‚ü®x, rfl‚ü©‚ü©‚ü©
        -- ‚ä¢ ‚àÉ h, Part.get (f (v, (PFun.fn f v x ::‚Çí v) none).fst) h = (v, (PFun.fn f v x ‚Ä¶
                                                                        -- üéâ no goals
#align dioph.dioph_pfun_comp1 Dioph.diophPfun_comp1

theorem diophFn_comp1 {S : Set (Option Œ± ‚Üí ‚Ñï)} (d : Dioph S) {f : (Œ± ‚Üí ‚Ñï) ‚Üí ‚Ñï} (df : DiophFn f) :
    Dioph {v | f v ::‚Çí v ‚àà S} :=
  ext (diophPfun_comp1 d <| cast (diophFn_iff_pFun f) df)
    fun _ => ‚ü®fun ‚ü®_, h‚ü© => h, fun h => ‚ü®trivial, h‚ü©‚ü©
#align dioph.dioph_fn_comp1 Dioph.diophFn_comp1

end

section

variable {Œ± Œ≤ : Type} {n : ‚Ñï}

open Vector3

open scoped Vector3

-- Porting note: Fails because declaration is in an imported module
-- attribute [local reducible] Vector3

theorem diophFn_vec_comp1 {S : Set (Vector3 ‚Ñï (succ n))} (d : Dioph S) {f : Vector3 ‚Ñï n ‚Üí ‚Ñï}
    (df : DiophFn f) : Dioph {v : Vector3 ‚Ñï n | (f v::v) ‚àà S} :=
  Dioph.ext (diophFn_comp1 (reindex_dioph _ (none::some) d) df) (fun v => by
    dsimp
    -- ‚ä¢ ((f v ::‚Çí v) ‚àò none :: some) ‚àà S ‚Üî v ‚àà {v | (f v :: v) ‚àà S}
    -- Porting note: `congr` use to be enough here
    refine iff_of_eq (congrFun (congrArg Membership.mem ?_) S)
    -- ‚ä¢ ((f v ::‚Çí v) ‚àò none :: some) = f v :: v
    ext x; cases x <;> rfl)
    -- ‚ä¢ ((f v ::‚Çí v) ‚àò none :: some) x = (f v :: v) x
           -- ‚ä¢ ((f v ::‚Çí v) ‚àò none :: some) fz = (f v :: v) fz
                       -- üéâ no goals
                       -- üéâ no goals
#align dioph.dioph_fn_vec_comp1 Dioph.diophFn_vec_comp1

theorem vec_ex1_dioph (n) {S : Set (Vector3 ‚Ñï (succ n))} (d : Dioph S) :
    Dioph {v : Fin2 n ‚Üí ‚Ñï | ‚àÉ x, (x::v) ‚àà S} :=
  ext (ex1_dioph <| reindex_dioph _ (none::some) d) fun v =>
    exists_congr fun x => by
      dsimp
      -- ‚ä¢ ((x ::‚Çí v) ‚àò none :: some) ‚àà S ‚Üî (x :: v) ‚àà S
      rw [show Option.elim' x v ‚àò cons none some = x::v from
          funext fun s => by cases' s with a b <;> rfl]
#align dioph.vec_ex1_dioph Dioph.vec_ex1_dioph

theorem diophFn_vec (f : Vector3 ‚Ñï n ‚Üí ‚Ñï) : DiophFn f ‚Üî Dioph {v | f (v ‚àò fs) = v fz} :=
  ‚ü®reindex_dioph _ (fz ::‚Çí fs), reindex_dioph _ (none::some)‚ü©
#align dioph.dioph_fn_vec Dioph.diophFn_vec

theorem diophPfun_vec (f : Vector3 ‚Ñï n ‚Üí. ‚Ñï) : DiophPfun f ‚Üî Dioph {v | f.graph (v ‚àò fs, v fz)} :=
  ‚ü®reindex_dioph _ (fz ::‚Çí fs), reindex_dioph _ (none::some)‚ü©
#align dioph.dioph_pfun_vec Dioph.diophPfun_vec

theorem diophFn_compn :
    ‚àÄ {n} {S : Set (Sum Œ± (Fin2 n) ‚Üí ‚Ñï)} (_ : Dioph S) {f : Vector3 ((Œ± ‚Üí ‚Ñï) ‚Üí ‚Ñï) n}
      (_ : VectorAllP DiophFn f), Dioph {v : Œ± ‚Üí ‚Ñï | (v ‚äó fun i => f i v) ‚àà S}
  | 0, S, d, f => fun _ =>
    ext (reindex_dioph _ (id ‚äó Fin2.elim0) d) fun v => by
      dsimp
      -- ‚ä¢ v ‚àò (id ‚äó elim0) ‚àà S ‚Üî (v ‚äó fun i => f i v) ‚àà S
      -- Porting note: `congr` use to be enough here
      refine iff_of_eq (congrFun (congrArg Membership.mem ?_) S)
      -- ‚ä¢ v ‚àò (id ‚äó elim0) = v ‚äó fun i => f i v
      ext x; obtain _ | _ | _ := x; rfl
      -- ‚ä¢ (v ‚àò (id ‚äó elim0)) x = (v ‚äó fun i => f i v) x
             -- ‚ä¢ (v ‚àò (id ‚äó elim0)) (inl val‚úù) = (v ‚äó fun i => f i v) (inl val‚úù)
                                    -- üéâ no goals
  | succ n, S, d, f =>
    f.consElim fun f fl => by
        simp
        -- ‚ä¢ DiophFn f ‚Üí VectorAllP DiophFn fl ‚Üí Dioph {v | (v ‚äó fun i => (f :: fl) i v)  ‚Ä¶
        exact fun df dfl =>
          have : Dioph {v | (v ‚àò inl ‚äó f (v ‚àò inl)::v ‚àò inr) ‚àà S} :=
            ext (diophFn_comp1 (reindex_dioph _ (some ‚àò inl ‚äó none::some ‚àò inr) d) <|
                reindex_diophFn inl df)
              fun v => by
                dsimp
                -- Porting note: `congr` use to be enough here
                refine iff_of_eq (congrFun (congrArg Membership.mem ?_) S)
                ext x; obtain _ | _ | _ := x <;> rfl
          have : Dioph {v | (v ‚äó f v::fun i : Fin2 n => fl i v) ‚àà S} :=
            @diophFn_compn n (fun v => S (v ‚àò inl ‚äó f (v ‚àò inl)::v ‚àò inr)) this _ dfl
          ext this fun v => by
            dsimp;
            -- Porting note: `congr` use to be enough here
            refine iff_of_eq (congrFun (congrArg Membership.mem ?_) S)
            ext x; obtain _ | _ | _ := x <;> rfl
#align dioph.dioph_fn_compn Dioph.diophFn_compn

theorem dioph_comp {S : Set (Vector3 ‚Ñï n)} (d : Dioph S) (f : Vector3 ((Œ± ‚Üí ‚Ñï) ‚Üí ‚Ñï) n)
    (df : VectorAllP DiophFn f) : Dioph {v | (fun i => f i v) ‚àà S} :=
  diophFn_compn (reindex_dioph _ inr d) df
#align dioph.dioph_comp Dioph.dioph_comp

theorem diophFn_comp {f : Vector3 ‚Ñï n ‚Üí ‚Ñï} (df : DiophFn f) (g : Vector3 ((Œ± ‚Üí ‚Ñï) ‚Üí ‚Ñï) n)
    (dg : VectorAllP DiophFn g) : DiophFn fun v => f fun i => g i v :=
  dioph_comp ((diophFn_vec _).1 df) ((fun v => v none)::fun i v => g i (v ‚àò some)) <| by
    simp
    -- ‚ä¢ (DiophFn fun v => v none) ‚àß VectorAllP DiophFn fun i v => g i (v ‚àò some)
    exact ‚ü®proj_dioph none, (vectorAllP_iff_forall _ _).2 fun i =>
          reindex_diophFn _ <| (vectorAllP_iff_forall _ _).1 dg _‚ü©
#align dioph.dioph_fn_comp Dioph.diophFn_comp

scoped notation:35 x " D‚àß " y => Dioph.inter x y

scoped notation:35 x " D‚à® " y => Dioph.union x y

scoped notation:30 "D‚àÉ" => Dioph.vec_ex1_dioph

scoped prefix:arg "&" => Fin2.ofNat'

theorem proj_dioph_of_nat {n : ‚Ñï} (m : ‚Ñï) [IsLT m n] : DiophFn fun v : Vector3 ‚Ñï n => v &m :=
  proj_dioph &m
#align dioph.proj_dioph_of_nat Dioph.proj_dioph_of_nat

scoped prefix:100 "D&" => Dioph.proj_dioph_of_nat

theorem const_dioph (n : ‚Ñï) : DiophFn (const (Œ± ‚Üí ‚Ñï) n) :=
  abs_poly_dioph (Poly.const n)
#align dioph.const_dioph Dioph.const_dioph

scoped prefix:100 "D." => Dioph.const_dioph

variable {f g : (Œ± ‚Üí ‚Ñï) ‚Üí ‚Ñï} (df : DiophFn f) (dg : DiophFn g)

theorem dioph_comp2 {S : ‚Ñï ‚Üí ‚Ñï ‚Üí Prop} (d : Dioph fun v : Vector3 ‚Ñï 2 => S (v &0) (v &1)) :
    Dioph fun v => S (f v) (g v) := dioph_comp d [f, g] ‚ü®df, dg‚ü©
#align dioph.dioph_comp2 Dioph.dioph_comp2

theorem diophFn_comp2 {h : ‚Ñï ‚Üí ‚Ñï ‚Üí ‚Ñï} (d : DiophFn fun v : Vector3 ‚Ñï 2 => h (v &0) (v &1)) :
    DiophFn fun v => h (f v) (g v) := diophFn_comp d [f, g] ‚ü®df, dg‚ü©
#align dioph.dioph_fn_comp2 Dioph.diophFn_comp2

theorem eq_dioph : Dioph fun v => f v = g v :=
  dioph_comp2 df dg <|
    of_no_dummies _ (Poly.proj &0 - Poly.proj &1) fun v => by
      exact Int.ofNat_inj.symm.trans ‚ü®@sub_eq_zero_of_eq ‚Ñ§ _ (v &0) (v &1), eq_of_sub_eq_zero‚ü©
      -- üéâ no goals
#align dioph.eq_dioph Dioph.eq_dioph

-- mathport name: eq_dioph
scoped infixl:50 " D= " => Dioph.eq_dioph

theorem add_dioph : DiophFn fun v => f v + g v :=
  diophFn_comp2 df dg <| abs_poly_dioph (@Poly.proj (Fin2 2) &0 + @Poly.proj (Fin2 2) &1)
#align dioph.add_dioph Dioph.add_dioph

-- mathport name: add_dioph
scoped infixl:80 " D+ " => Dioph.add_dioph

theorem mul_dioph : DiophFn fun v => f v * g v :=
  diophFn_comp2 df dg <| abs_poly_dioph (@Poly.proj (Fin2 2) &0 * @Poly.proj (Fin2 2) &1)
#align dioph.mul_dioph Dioph.mul_dioph

-- mathport name: mul_dioph
scoped infixl:90 " D* " => Dioph.mul_dioph

theorem le_dioph : Dioph {v | f v ‚â§ g v} :=
  dioph_comp2 df dg <|
    ext ((D‚àÉ) 2 <| D&1 D+ D&0 D= D&2) fun _ => ‚ü®fun ‚ü®_, hx‚ü© => le.intro hx, le.dest‚ü©
#align dioph.le_dioph Dioph.le_dioph

scoped infixl:50 " D‚â§ " => Dioph.le_dioph

theorem lt_dioph : Dioph {v | f v < g v} := df D+ D.1 D‚â§ dg
#align dioph.lt_dioph Dioph.lt_dioph

scoped infixl:50 " D< " => Dioph.lt_dioph

theorem ne_dioph : Dioph {v | f v ‚â† g v} :=
  ext (df D< dg D‚à® dg D< df) fun v => by dsimp; exact lt_or_lt_iff_ne (Œ± := ‚Ñï)
                                         -- ‚ä¢ v ‚àà {v | f v < g v} ‚à™ {v | g v < f v} ‚Üî ¬¨f v = g v
                                                -- üéâ no goals
#align dioph.ne_dioph Dioph.ne_dioph

scoped infixl:50 " D‚â† " => Dioph.ne_dioph

theorem sub_dioph : DiophFn fun v => f v - g v :=
  diophFn_comp2 df dg <|
    (diophFn_vec _).2 <|
      ext (D&1 D= D&0 D+ D&2 D‚à® D&1 D‚â§ D&2 D‚àß D&0 D= D.0) <|
        (vectorAll_iff_forall _).1 fun x y z =>
          show y = x + z ‚à® y ‚â§ z ‚àß x = 0 ‚Üî y - z = x from
            ‚ü®fun o => by
              rcases o with (ae | ‚ü®yz, x0‚ü©)
              -- ‚ä¢ y - z = x
              ¬∑ rw [ae, add_tsub_cancel_right]
                -- üéâ no goals
              ¬∑ rw [x0, tsub_eq_zero_iff_le.mpr yz], by
                -- üéâ no goals
              rintro rfl
              -- ‚ä¢ y = y - z + z ‚à® y ‚â§ z ‚àß y - z = 0
              cases' le_total y z with yz zy
              -- ‚ä¢ y = y - z + z ‚à® y ‚â§ z ‚àß y - z = 0
              ¬∑ exact Or.inr ‚ü®yz, tsub_eq_zero_iff_le.mpr yz‚ü©
                -- üéâ no goals
              ¬∑ exact Or.inl (tsub_add_cancel_of_le zy).symm‚ü©
                -- üéâ no goals
#align dioph.sub_dioph Dioph.sub_dioph

scoped infixl:80 " D- " => Dioph.sub_dioph

theorem dvd_dioph : Dioph fun v => f v ‚à£ g v :=
  dioph_comp ((D‚àÉ) 2 <| D&2 D= D&1 D* D&0) [f, g] ‚ü®df, dg‚ü©
#align dioph.dvd_dioph Dioph.dvd_dioph

scoped infixl:50 " D‚à£ " => Dioph.dvd_dioph

theorem mod_dioph : DiophFn fun v => f v % g v :=
  have : Dioph fun v : Vector3 ‚Ñï 3 => (v &2 = 0 ‚à® v &0 < v &2) ‚àß ‚àÉ x : ‚Ñï, v &0 + v &2 * x = v &1 :=
    (D&2 D= D.0 D‚à® D&0 D< D&2) D‚àß (D‚àÉ) 3 <| D&1 D+ D&3 D* D&0 D= D&2
  diophFn_comp2 df dg <|
    (diophFn_vec _).2 <|
      ext this <|
        (vectorAll_iff_forall _).1 fun z x y =>
          show ((y = 0 ‚à® z < y) ‚àß ‚àÉ c, z + y * c = x) ‚Üî x % y = z from
            ‚ü®fun ‚ü®h, c, hc‚ü© => by
              rw [‚Üê hc]; simp; cases' h with x0 hl; rw [x0, mod_zero]
              -- ‚ä¢ (z + y * c) % y = z
                         -- ‚ä¢ z % y = z
                               -- ‚ä¢ z % y = z
                                                    -- ‚ä¢ z % y = z
              exact mod_eq_of_lt hl, fun e => by
              -- üéâ no goals
                rw [‚Üê e]
                -- ‚ä¢ (y = 0 ‚à® x % y < y) ‚àß ‚àÉ c, x % y + y * c = x
                exact ‚ü®or_iff_not_imp_left.2 fun h => mod_lt _ (Nat.pos_of_ne_zero h), x / y,
                  mod_add_div _ _‚ü©‚ü©
#align dioph.mod_dioph Dioph.mod_dioph

scoped infixl:80 " D% " => Dioph.mod_dioph

theorem modEq_dioph {h : (Œ± ‚Üí ‚Ñï) ‚Üí ‚Ñï} (dh : DiophFn h) : Dioph fun v => f v ‚â° g v [MOD h v] :=
  df D% dh D= dg D% dh
#align dioph.modeq_dioph Dioph.modEq_dioph

scoped notation " D‚â° " => Dioph.modEq_dioph

theorem div_dioph : DiophFn fun v => f v / g v :=
  have :
    Dioph fun v : Vector3 ‚Ñï 3 =>
      v &2 = 0 ‚àß v &0 = 0 ‚à® v &0 * v &2 ‚â§ v &1 ‚àß v &1 < (v &0 + 1) * v &2 :=
    (D&2 D= D.0 D‚àß D&0 D= D.0) D‚à® D&0 D* D&2 D‚â§ D&1 D‚àß D&1 D< (D&0 D+ D.1) D* D&2
  diophFn_comp2 df dg <|
    (diophFn_vec _).2 <|
      ext this <|
        (vectorAll_iff_forall _).1 fun z x y =>
          show y = 0 ‚àß z = 0 ‚à® z * y ‚â§ x ‚àß x < (z + 1) * y ‚Üî x / y = z by
            refine Iff.trans ?_ eq_comm
            -- ‚ä¢ y = 0 ‚àß z = 0 ‚à® z * y ‚â§ x ‚àß x < (z + 1) * y ‚Üî z = x / y
            exact y.eq_zero_or_pos.elim
              (fun y0 => by
                rw [y0, Nat.div_zero]
                exact ‚ü®fun o => (o.resolve_right fun ‚ü®_, h2‚ü© => Nat.not_lt_zero _ h2).right,
                  fun z0 => Or.inl ‚ü®rfl, z0‚ü©‚ü©)
              fun ypos =>
                Iff.trans ‚ü®fun o => o.resolve_left fun ‚ü®h1, _‚ü© => Nat.ne_of_gt ypos h1, Or.inr‚ü©
                  (le_antisymm_iff.trans <| and_congr (Nat.le_div_iff_mul_le ypos) <|
                    Iff.trans ‚ü®lt_succ_of_le, le_of_lt_succ‚ü© (div_lt_iff_lt_mul ypos)).symm
#align dioph.div_dioph Dioph.div_dioph

-- mathport name: div_dioph
scoped infixl:80 " D/ " => Dioph.div_dioph

open Pell

theorem pell_dioph :
    Dioph fun v : Vector3 ‚Ñï 4 => ‚àÉ h : 1 < v &0, xn h (v &1) = v &2 ‚àß yn h (v &1) = v &3 := by
  have proof := D.1 D< D&0 D‚àß D&1 D‚â§ D&3 D‚àß
      ((D&2 D= D.1 D‚àß D&3 D= D.0) D‚à®
      ((D‚àÉ) 4 <| (D‚àÉ) 5 <| (D‚àÉ) 6 <| (D‚àÉ) 7 <| (D‚àÉ) 8 <|
      D&7 D* D&7 D- (D&5 D* D&5 D- D.1) D* D&8 D* D&8 D= D.1 D‚àß
      D&4 D* D&4 D- (D&5 D* D&5 D- D.1) D* D&3 D* D&3 D= D.1 D‚àß
      D&2 D* D&2 D- (D&0 D* D&0 D- D.1) D* D&1 D* D&1 D= D.1 D‚àß
      D.1 D< D&0 D‚àß (D‚â° (D&0) (D.1) (D.4 D* D&8)) D‚àß (D‚â° (D&0) (D&5) (D&4)) D‚àß
      D.0 D< D&3 D‚àß D&8 D* D&8 D‚à£ D&3 D‚àß
      (D‚â° (D&2) (D&7) (D&4)) D‚àß
      (D‚â° (D&1) (D&6) (D.4 D* (D&8)))))
  -- Porting note: copying directly `proof` in the proof of the following have fails
  have : Dioph {v : Vector3 ‚Ñï 4 |
    1 < v &0 ‚àß v &1 ‚â§ v &3 ‚àß
    (v &2 = 1 ‚àß v &3 = 0 ‚à®
    ‚àÉ u w s t b : ‚Ñï,
      v &2 * v &2 - (v &0 * v &0 - 1) * v &3 * v &3 = 1 ‚àß
      u * u - (v &0 * v &0 - 1) * w * w = 1 ‚àß
      s * s - (b * b - 1) * t * t = 1 ‚àß
      1 < b ‚àß b ‚â° 1 [MOD 4 * v &3] ‚àß b ‚â° v &0 [MOD u] ‚àß
      0 < w ‚àß v &3 * v &3 ‚à£ w ‚àß
      s ‚â° v &2 [MOD u] ‚àß
      t ‚â° v &1 [MOD 4 * v &3])} := by
    exact proof
  exact Dioph.ext this fun v => matiyasevic.symm
  -- üéâ no goals
#align dioph.pell_dioph Dioph.pell_dioph

theorem xn_dioph : DiophPfun fun v : Vector3 ‚Ñï 2 => ‚ü®1 < v &0, fun h => xn h (v &1)‚ü© :=
  have : Dioph fun v : Vector3 ‚Ñï 3 => ‚àÉ y, ‚àÉ h : 1 < v &1, xn h (v &2) = v &0 ‚àß yn h (v &2) = y :=
    let D_pell := pell_dioph.reindex_dioph (Fin2 4) [&2, &3, &1, &0]
    (D‚àÉ) 3 D_pell
  (diophPfun_vec _).2 <|
    Dioph.ext this fun _ => ‚ü®fun ‚ü®_, h, xe, _‚ü© => ‚ü®h, xe‚ü©, fun ‚ü®h, xe‚ü© => ‚ü®_, h, xe, rfl‚ü©‚ü©
#align dioph.xn_dioph Dioph.xn_dioph

/-- A version of **Matiyasevic's theorem** -/
theorem pow_dioph : DiophFn fun v => f v ^ g v := by
  have proof :=
    let D_pell := pell_dioph.reindex_dioph (Fin2 9) [&4, &8, &1, &0]
    (D&2 D= D.0 D‚àß D&0 D= D.1) D‚à® (D.0 D< D&2 D‚àß
    ((D&1 D= D.0 D‚àß D&0 D= D.0) D‚à® (D.0 D< D&1 D‚àß
    ((D‚àÉ) 3 <| (D‚àÉ) 4 <| (D‚àÉ) 5 <| (D‚àÉ) 6 $ (D‚àÉ) 7 <| (D‚àÉ) 8 <| D_pell D‚àß
    (D‚â° (D&1) (D&0 D* (D&4 D- D&7) D+ D&6) (D&3)) D‚àß
    D.2 D* D&4 D* D&7 D= D&3 D+ (D&7 D* D&7 D+ D.1) D‚àß
    D&6 D< D&3 D‚àß D&7 D‚â§ D&5 D‚àß D&8 D‚â§ D&5 D‚àß
    D&4 D* D&4 D- ((D&5 D+ D.1) D* (D&5 D+ D.1) D- D.1) D* (D&5 D* D&2) D* (D&5 D* D&2) D= D.1))))
  -- Porting note: copying directly `proof` in the proof of the following have fails
  have : Dioph {v : Vector3 ‚Ñï 3 |
    v &2 = 0 ‚àß v &0 = 1 ‚à® 0 < v &2 ‚àß
    (v &1 = 0 ‚àß v &0 = 0 ‚à® 0 < v &1 ‚àß
    ‚àÉ w a t z x y : ‚Ñï,
      (‚àÉ a1 : 1 < a, xn a1 (v &2) = x ‚àß yn a1 (v &2) = y) ‚àß
      x ‚â° y * (a - v &1) + v &0 [MOD t] ‚àß
      2 * a * v &1 = t + (v &1 * v &1 + 1) ‚àß
      v &0 < t ‚àß v &1 ‚â§ w ‚àß v &2 ‚â§ w ‚àß
      a * a - ((w + 1) * (w + 1) - 1) * (w * z) * (w * z) = 1)} := by
    exact proof
  exact diophFn_comp2 df dg <| (diophFn_vec _).2 <| Dioph.ext this fun v => Iff.symm <|
    eq_pow_of_pell.trans <| or_congr Iff.rfl <| and_congr Iff.rfl <| or_congr Iff.rfl <|
       and_congr Iff.rfl <|
        ‚ü®fun ‚ü®w, a, t, z, a1, h‚ü© => ‚ü®w, a, t, z, _, _, ‚ü®a1, rfl, rfl‚ü©, h‚ü©,
        fun ‚ü®w, a, t, z, _, _, ‚ü®a1, rfl, rfl‚ü©, h‚ü© => ‚ü®w, a, t, z, a1, h‚ü©‚ü©
#align dioph.pow_dioph Dioph.pow_dioph

end

end Dioph
