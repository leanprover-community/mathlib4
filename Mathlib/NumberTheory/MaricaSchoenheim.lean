/-
Copyright (c) 2023 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Combinatorics.SetFamily.FourFunctions
import Mathlib.Data.Nat.Squarefree

/-!
# The Marica-SchÃ¶nheim special case of Graham's conjecture

Graham's conjecture states that if $0 < a_1 < \dots a_n$ are integers, then
$\max_{i, j} \frac{a_i}{\gcd(a_i, a_j)} \ge n$. This file proves the conjecture when the $a_i$ are
squarefree as a corollary of the Marica-SchÃ¶nheim inequality.

## References

[*Applications of the FKG Inequality and Its Relatives*, Graham][Graham1983]
-/

open Finset
open scoped FinsetFamily

namespace Nat

/-- Statement of Graham's conjecture (which is now a theorem in the literature).

Graham's conjecture states that if $0 < a_1 < \dots a_n$ are integers, then
$\max_{i, j} \frac{a_i}{\gcd(a_i, a_j)} \ge n$. -/
def GrahamConjecture (n : â„•) (f : â„• â†’ â„•) : Prop :=
  n â‰  0 â†’ StrictMonoOn f (Set.Iio n) â†’ âˆƒ i < n, âˆƒ j < n, (f i).gcd (f j) * n â‰¤ f i

/-- The special case of Graham's conjecture where all numbers are squarefree. -/
lemma grahamConjecture_of_squarefree {n : â„•} (f : â„• â†’ â„•) (hf' : âˆ€ k < n, Squarefree (f k)) :
    GrahamConjecture n f := by
  rintro hn hf
  by_contra!
  set ğ’œ := (Iio n).image fun n â†¦ primeFactors (f n)
  have hf'' : âˆ€ i < n, âˆ€ j, Squarefree (f i / (f i).gcd (f j)) :=
    fun i hi j â†¦ (hf' _ hi).squarefree_of_dvd <| div_dvd_of_dvd <| gcd_dvd_left _ _
  refine lt_irrefl n ?_
  calc
    n = #ğ’œ := ?_
    _ â‰¤ #(ğ’œ \\ ğ’œ) := ğ’œ.card_le_card_diffs
    _ â‰¤ #(Ioo 0 n) := card_le_card_of_injOn (fun s â†¦ âˆ p âˆˆ s, p) ?_ ?_
    _ = n - 1 := by rw [card_Ioo, tsub_zero]
    _ < n := tsub_lt_self hn.bot_lt zero_lt_one
  Â· rw [Finset.card_image_of_injOn, card_Iio]
    simpa using prod_primeFactors_invOn_squarefree.2.injOn.comp hf.injOn hf'
  Â· simp only [ğ’œ, forall_mem_diffs, forall_mem_image, mem_Ioo, mem_Iio, Set.MapsTo, mem_coe]
    rintro i hi j hj
    rw [â† primeFactors_div_gcd (hf' _ hi) (hf' _ hj).ne_zero,
      prod_primeFactors_of_squarefree <| hf'' _ hi _]
    exact âŸ¨Nat.div_pos (gcd_le_left _ (hf' _ hi).ne_zero.bot_lt) <|
      Nat.gcd_pos_of_pos_left _ (hf' _ hi).ne_zero.bot_lt, Nat.div_lt_of_lt_mul <| this _ hi _ hjâŸ©
  Â· simp only [ğ’œ, Set.InjOn, mem_coe, forall_mem_diffs, forall_mem_image, mem_Iio]
    rintro a ha b hb c hc d hd
    rw [â† primeFactors_div_gcd (hf' _ ha) (hf' _ hb).ne_zero, â† primeFactors_div_gcd
      (hf' _ hc) (hf' _ hd).ne_zero, prod_primeFactors_of_squarefree (hf'' _ ha _),
      prod_primeFactors_of_squarefree (hf'' _ hc _)]
    rintro h
    rw [h]

end Nat
