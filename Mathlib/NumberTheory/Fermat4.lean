/-
Copyright (c) 2020 Paul van Wamelen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul van Wamelen
-/
import Mathlib.NumberTheory.PythagoreanTriples
import Mathlib.RingTheory.Coprime.Lemmas
import Mathlib.Tactic.LinearCombination

#align_import number_theory.fermat4 from "leanprover-community/mathlib"@"10b4e499f43088dd3bb7b5796184ad5216648ab1"

/-!
# Fermat's Last Theorem for the case n = 4
There are no non-zero integers `a`, `b` and `c` such that `a ^ 4 + b ^ 4 = c ^ 4`.
-/


noncomputable section

open Classical

/-- Shorthand for three non-zero integers `a`, `b`, and `c` satisfying `a ^ 4 + b ^ 4 = c ^ 2`.
We will show that no integers satisfy this equation. Clearly Fermat's Last theorem for n = 4
follows. -/
def Fermat42 (a b c : â„¤) : Prop :=
  a â‰  0 âˆ§ b â‰  0 âˆ§ a ^ 4 + b ^ 4 = c ^ 2
#align fermat_42 Fermat42

namespace Fermat42

theorem comm {a b c : â„¤} : Fermat42 a b c â†” Fermat42 b a c := by
  delta Fermat42
  -- âŠ¢ a â‰  0 âˆ§ b â‰  0 âˆ§ a ^ 4 + b ^ 4 = c ^ 2 â†” b â‰  0 âˆ§ a â‰  0 âˆ§ b ^ 4 + a ^ 4 = c ^ 2
  rw [add_comm]
  -- âŠ¢ a â‰  0 âˆ§ b â‰  0 âˆ§ b ^ 4 + a ^ 4 = c ^ 2 â†” b â‰  0 âˆ§ a â‰  0 âˆ§ b ^ 4 + a ^ 4 = c ^ 2
  tauto
  -- ğŸ‰ no goals
#align fermat_42.comm Fermat42.comm

theorem mul {a b c k : â„¤} (hk0 : k â‰  0) :
    Fermat42 a b c â†” Fermat42 (k * a) (k * b) (k ^ 2 * c) := by
  delta Fermat42
  -- âŠ¢ a â‰  0 âˆ§ b â‰  0 âˆ§ a ^ 4 + b ^ 4 = c ^ 2 â†” k * a â‰  0 âˆ§ k * b â‰  0 âˆ§ (k * a) ^ 4  â€¦
  constructor
  -- âŠ¢ a â‰  0 âˆ§ b â‰  0 âˆ§ a ^ 4 + b ^ 4 = c ^ 2 â†’ k * a â‰  0 âˆ§ k * b â‰  0 âˆ§ (k * a) ^ 4  â€¦
  Â· intro f42
    -- âŠ¢ k * a â‰  0 âˆ§ k * b â‰  0 âˆ§ (k * a) ^ 4 + (k * b) ^ 4 = (k ^ 2 * c) ^ 2
    constructor
    -- âŠ¢ k * a â‰  0
    Â· exact mul_ne_zero hk0 f42.1
      -- ğŸ‰ no goals
    constructor
    -- âŠ¢ k * b â‰  0
    Â· exact mul_ne_zero hk0 f42.2.1
      -- ğŸ‰ no goals
    Â· have H : a ^ 4 + b ^ 4 = c ^ 2 := f42.2.2
      -- âŠ¢ (k * a) ^ 4 + (k * b) ^ 4 = (k ^ 2 * c) ^ 2
      linear_combination k ^ 4 * H
      -- ğŸ‰ no goals
  Â· intro f42
    -- âŠ¢ a â‰  0 âˆ§ b â‰  0 âˆ§ a ^ 4 + b ^ 4 = c ^ 2
    constructor
    -- âŠ¢ a â‰  0
    Â· exact right_ne_zero_of_mul f42.1
      -- ğŸ‰ no goals
    constructor
    -- âŠ¢ b â‰  0
    Â· exact right_ne_zero_of_mul f42.2.1
      -- ğŸ‰ no goals
    apply (mul_right_inj' (pow_ne_zero 4 hk0)).mp
    -- âŠ¢ k ^ 4 * (a ^ 4 + b ^ 4) = k ^ 4 * c ^ 2
    linear_combination f42.2.2
    -- ğŸ‰ no goals
#align fermat_42.mul Fermat42.mul

theorem ne_zero {a b c : â„¤} (h : Fermat42 a b c) : c â‰  0 := by
  apply ne_zero_pow two_ne_zero _; apply ne_of_gt
  -- âŠ¢ c ^ 2 â‰  0
                                   -- âŠ¢ 0 < c ^ 2
  rw [â† h.2.2, (by ring : a ^ 4 + b ^ 4 = (a ^ 2) ^ 2 + (b ^ 2) ^ 2)]
  -- âŠ¢ 0 < (a ^ 2) ^ 2 + (b ^ 2) ^ 2
  exact
    add_pos (sq_pos_of_ne_zero _ (pow_ne_zero 2 h.1)) (sq_pos_of_ne_zero _ (pow_ne_zero 2 h.2.1))
#align fermat_42.ne_zero Fermat42.ne_zero

/-- We say a solution to `a ^ 4 + b ^ 4 = c ^ 2` is minimal if there is no other solution with
a smaller `c` (in absolute value). -/
def Minimal (a b c : â„¤) : Prop :=
  Fermat42 a b c âˆ§ âˆ€ a1 b1 c1 : â„¤, Fermat42 a1 b1 c1 â†’ Int.natAbs c â‰¤ Int.natAbs c1
#align fermat_42.minimal Fermat42.Minimal

/-- if we have a solution to `a ^ 4 + b ^ 4 = c ^ 2` then there must be a minimal one. -/
theorem exists_minimal {a b c : â„¤} (h : Fermat42 a b c) : âˆƒ a0 b0 c0, Minimal a0 b0 c0 := by
  let S : Set â„• := { n | âˆƒ s : â„¤ Ã— â„¤ Ã— â„¤, Fermat42 s.1 s.2.1 s.2.2 âˆ§ n = Int.natAbs s.2.2 }
  -- âŠ¢ âˆƒ a0 b0 c0, Minimal a0 b0 c0
  have S_nonempty : S.Nonempty := by
    use Int.natAbs c
    rw [Set.mem_setOf_eq]
    use âŸ¨a, âŸ¨b, câŸ©âŸ©
  let m : â„• := Nat.find S_nonempty
  -- âŠ¢ âˆƒ a0 b0 c0, Minimal a0 b0 c0
  have m_mem : m âˆˆ S := Nat.find_spec S_nonempty
  -- âŠ¢ âˆƒ a0 b0 c0, Minimal a0 b0 c0
  rcases m_mem with âŸ¨s0, hs0, hs1âŸ©
  -- âŠ¢ âˆƒ a0 b0 c0, Minimal a0 b0 c0
  use s0.1, s0.2.1, s0.2.2, hs0
  -- âŠ¢ âˆ€ (a1 b1 c1 : â„¤), Fermat42 a1 b1 c1 â†’ Int.natAbs s0.snd.snd â‰¤ Int.natAbs c1
  intro a1 b1 c1 h1
  -- âŠ¢ Int.natAbs s0.snd.snd â‰¤ Int.natAbs c1
  rw [â† hs1]
  -- âŠ¢ m â‰¤ Int.natAbs c1
  apply Nat.find_min'
  -- âŠ¢ Int.natAbs c1 âˆˆ S
  use âŸ¨a1, âŸ¨b1, c1âŸ©âŸ©
  -- ğŸ‰ no goals
#align fermat_42.exists_minimal Fermat42.exists_minimal

/-- a minimal solution to `a ^ 4 + b ^ 4 = c ^ 2` must have `a` and `b` coprime. -/
theorem coprime_of_minimal {a b c : â„¤} (h : Minimal a b c) : IsCoprime a b := by
  apply Int.gcd_eq_one_iff_coprime.mp
  -- âŠ¢ Int.gcd a b = 1
  by_contra hab
  -- âŠ¢ False
  obtain âŸ¨p, hp, hpa, hpbâŸ© := Nat.Prime.not_coprime_iff_dvd.mp hab
  -- âŠ¢ False
  obtain âŸ¨a1, rflâŸ© := Int.coe_nat_dvd_left.mpr hpa
  -- âŠ¢ False
  obtain âŸ¨b1, rflâŸ© := Int.coe_nat_dvd_left.mpr hpb
  -- âŠ¢ False
  have hpc : (p : â„¤) ^ 2 âˆ£ c := by
    rw [â† Int.pow_dvd_pow_iff zero_lt_two, â† h.1.2.2]
    apply Dvd.intro (a1 ^ 4 + b1 ^ 4)
    ring
  obtain âŸ¨c1, rflâŸ© := hpc
  -- âŠ¢ False
  have hf : Fermat42 a1 b1 c1 :=
    (Fermat42.mul (Int.coe_nat_ne_zero.mpr (Nat.Prime.ne_zero hp))).mpr h.1
  apply Nat.le_lt_antisymm (h.2 _ _ _ hf)
  -- âŠ¢ Int.natAbs c1 < Int.natAbs (â†‘p ^ 2 * c1)
  rw [Int.natAbs_mul, lt_mul_iff_one_lt_left, Int.natAbs_pow, Int.natAbs_ofNat]
  -- âŠ¢ 1 < p ^ 2
  Â· exact Nat.one_lt_pow _ _ zero_lt_two (Nat.Prime.one_lt hp)
    -- ğŸ‰ no goals
  Â· exact Nat.pos_of_ne_zero (Int.natAbs_ne_zero.2 (ne_zero hf))
    -- ğŸ‰ no goals
#align fermat_42.coprime_of_minimal Fermat42.coprime_of_minimal

/-- We can swap `a` and `b` in a minimal solution to `a ^ 4 + b ^ 4 = c ^ 2`. -/
theorem minimal_comm {a b c : â„¤} : Minimal a b c â†’ Minimal b a c := fun âŸ¨h1, h2âŸ© =>
  âŸ¨Fermat42.comm.mp h1, h2âŸ©
#align fermat_42.minimal_comm Fermat42.minimal_comm

/-- We can assume that a minimal solution to `a ^ 4 + b ^ 4 = c ^ 2` has positive `c`. -/
theorem neg_of_minimal {a b c : â„¤} : Minimal a b c â†’ Minimal a b (-c) := by
  rintro âŸ¨âŸ¨ha, hb, heqâŸ©, h2âŸ©
  -- âŠ¢ Minimal a b (-c)
  constructor
  -- âŠ¢ Fermat42 a b (-c)
  Â· apply And.intro ha (And.intro hb _)
    -- âŠ¢ a ^ 4 + b ^ 4 = (-c) ^ 2
    rw [heq]
    -- âŠ¢ c ^ 2 = (-c) ^ 2
    exact (neg_sq c).symm
    -- ğŸ‰ no goals
  rwa [Int.natAbs_neg c]
  -- ğŸ‰ no goals
#align fermat_42.neg_of_minimal Fermat42.neg_of_minimal

/-- We can assume that a minimal solution to `a ^ 4 + b ^ 4 = c ^ 2` has `a` odd. -/
theorem exists_odd_minimal {a b c : â„¤} (h : Fermat42 a b c) :
    âˆƒ a0 b0 c0, Minimal a0 b0 c0 âˆ§ a0 % 2 = 1 := by
  obtain âŸ¨a0, b0, c0, hfâŸ© := exists_minimal h
  -- âŠ¢ âˆƒ a0 b0 c0, Minimal a0 b0 c0 âˆ§ a0 % 2 = 1
  cases' Int.emod_two_eq_zero_or_one a0 with hap hap
  -- âŠ¢ âˆƒ a0 b0 c0, Minimal a0 b0 c0 âˆ§ a0 % 2 = 1
  Â· cases' Int.emod_two_eq_zero_or_one b0 with hbp hbp
    -- âŠ¢ âˆƒ a0 b0 c0, Minimal a0 b0 c0 âˆ§ a0 % 2 = 1
    Â· exfalso
      -- âŠ¢ False
      have h1 : 2 âˆ£ (Int.gcd a0 b0 : â„¤) :=
        Int.dvd_gcd (Int.dvd_of_emod_eq_zero hap) (Int.dvd_of_emod_eq_zero hbp)
      rw [Int.gcd_eq_one_iff_coprime.mpr (coprime_of_minimal hf)] at h1
      -- âŠ¢ False
      revert h1
      -- âŠ¢ 2 âˆ£ â†‘1 â†’ False
      norm_num
      -- ğŸ‰ no goals
    Â· exact âŸ¨b0, âŸ¨a0, âŸ¨c0, minimal_comm hf, hbpâŸ©âŸ©âŸ©
      -- ğŸ‰ no goals
  exact âŸ¨a0, âŸ¨b0, âŸ¨c0, hf, hapâŸ©âŸ©âŸ©
  -- ğŸ‰ no goals
#align fermat_42.exists_odd_minimal Fermat42.exists_odd_minimal

/-- We can assume that a minimal solution to `a ^ 4 + b ^ 4 = c ^ 2` has
`a` odd and `c` positive. -/
theorem exists_pos_odd_minimal {a b c : â„¤} (h : Fermat42 a b c) :
    âˆƒ a0 b0 c0, Minimal a0 b0 c0 âˆ§ a0 % 2 = 1 âˆ§ 0 < c0 := by
  obtain âŸ¨a0, b0, c0, hf, hcâŸ© := exists_odd_minimal h
  -- âŠ¢ âˆƒ a0 b0 c0, Minimal a0 b0 c0 âˆ§ a0 % 2 = 1 âˆ§ 0 < c0
  rcases lt_trichotomy 0 c0 with (h1 | h1 | h1)
  Â· use a0, b0, c0
    -- ğŸ‰ no goals
  Â· exfalso
    -- âŠ¢ False
    exact ne_zero hf.1 h1.symm
    -- ğŸ‰ no goals
  Â· use a0, b0, -c0, neg_of_minimal hf, hc
    -- âŠ¢ 0 < -c0
    exact neg_pos.mpr h1
    -- ğŸ‰ no goals
#align fermat_42.exists_pos_odd_minimal Fermat42.exists_pos_odd_minimal

end Fermat42

theorem Int.coprime_of_sq_sum {r s : â„¤} (h2 : IsCoprime s r) : IsCoprime (r ^ 2 + s ^ 2) r := by
  rw [sq, sq]
  -- âŠ¢ IsCoprime (r * r + s * s) r
  exact (IsCoprime.mul_left h2 h2).mul_add_left_left r
  -- ğŸ‰ no goals
#align int.coprime_of_sq_sum Int.coprime_of_sq_sum

theorem Int.coprime_of_sq_sum' {r s : â„¤} (h : IsCoprime r s) :
    IsCoprime (r ^ 2 + s ^ 2) (r * s) := by
  apply IsCoprime.mul_right (Int.coprime_of_sq_sum (isCoprime_comm.mp h))
  -- âŠ¢ IsCoprime (r ^ 2 + s ^ 2) s
  rw [add_comm]; apply Int.coprime_of_sq_sum h
  -- âŠ¢ IsCoprime (s ^ 2 + r ^ 2) s
                 -- ğŸ‰ no goals
#align int.coprime_of_sq_sum' Int.coprime_of_sq_sum'

namespace Fermat42

-- If we have a solution to a ^ 4 + b ^ 4 = c ^ 2, we can construct a smaller one. This
-- implies there can't be a smallest solution.
theorem not_minimal {a b c : â„¤} (h : Minimal a b c) (ha2 : a % 2 = 1) (hc : 0 < c) : False := by
  -- Use the fact that a ^ 2, b ^ 2, c form a pythagorean triple to obtain m and n such that
  -- a ^ 2 = m ^ 2 - n ^ 2, b ^ 2 = 2 * m * n and c = m ^ 2 + n ^ 2
  -- first the formula:
  have ht : PythagoreanTriple (a ^ 2) (b ^ 2) c := by
    delta PythagoreanTriple
    linear_combination h.1.2.2
  -- coprime requirement:
  have h2 : Int.gcd (a ^ 2) (b ^ 2) = 1 := Int.gcd_eq_one_iff_coprime.mpr (coprime_of_minimal h).pow
  -- âŠ¢ False
  -- in order to reduce the possibilities we get from the classification of pythagorean triples
  -- it helps if we know the parity of a ^ 2 (and the sign of c):
  have ha22 : a ^ 2 % 2 = 1 := by
    rw [sq, Int.mul_emod, ha2]
    norm_num
  obtain âŸ¨m, n, ht1, ht2, ht3, ht4, ht5, ht6âŸ© := ht.coprime_classification' h2 ha22 hc
  -- âŠ¢ False
  -- Now a, n, m form a pythagorean triple and so we can obtain r and s such that
  -- a = r ^ 2 - s ^ 2, n = 2 * r * s and m = r ^ 2 + s ^ 2
  -- formula:
  have htt : PythagoreanTriple a n m := by
    delta PythagoreanTriple
    linear_combination ht1
  -- a and n are coprime, because a ^ 2 = m ^ 2 - n ^ 2 and m and n are coprime.
  have h3 : Int.gcd a n = 1 := by
    apply Int.gcd_eq_one_iff_coprime.mpr
    apply @IsCoprime.of_mul_left_left _ _ _ a
    rw [â† sq, ht1, (by ring : m ^ 2 - n ^ 2 = m ^ 2 + -n * n)]
    exact (Int.gcd_eq_one_iff_coprime.mp ht4).pow_left.add_mul_right_left (-n)
  -- m is positive because b is non-zero and b ^ 2 = 2 * m * n and we already have 0 â‰¤ m.
  have hb20 : b ^ 2 â‰  0 := mt pow_eq_zero h.1.2.1
  -- âŠ¢ False
  have h4 : 0 < m := by
    apply lt_of_le_of_ne ht6
    rintro rfl
    revert hb20
    rw [ht2]
    simp
  obtain âŸ¨r, s, _, htt2, htt3, htt4, htt5, htt6âŸ© := htt.coprime_classification' h3 ha2 h4
  -- âŠ¢ False
  -- Now use the fact that (b / 2) ^ 2 = m * r * s, and m, r and s are pairwise coprime to obtain
  -- i, j and k such that m = i ^ 2, r = j ^ 2 and s = k ^ 2.
  -- m and r * s are coprime because m = r ^ 2 + s ^ 2 and r and s are coprime.
  have hcp : Int.gcd m (r * s) = 1 := by
    rw [htt3]
    exact
      Int.gcd_eq_one_iff_coprime.mpr (Int.coprime_of_sq_sum' (Int.gcd_eq_one_iff_coprime.mp htt4))
  -- b is even because b ^ 2 = 2 * m * n.
  have hb2 : 2 âˆ£ b := by
    apply @Int.Prime.dvd_pow' _ 2 _ Nat.prime_two
    rw [ht2, mul_assoc]
    exact dvd_mul_right 2 (m * n)
  cases' hb2 with b' hb2'
  -- âŠ¢ False
  have hs : b' ^ 2 = m * (r * s) := by
    apply (mul_right_inj' (by norm_num : (4 : â„¤) â‰  0)).mp
    linear_combination (-b - 2 * b') * hb2' + ht2 + 2 * m * htt2
  have hrsz : r * s â‰  0 := by
    -- because b ^ 2 is not zero and (b / 2) ^ 2 = m * (r * s)
    by_contra hrsz
    revert hb20
    rw [ht2, htt2, mul_assoc, @mul_assoc _ _ _ r s, hrsz]
    simp
  have h2b0 : b' â‰  0 := by
    apply ne_zero_pow two_ne_zero
    rw [hs]
    apply mul_ne_zero
    Â· exact ne_of_gt h4
    Â· exact hrsz
  obtain âŸ¨i, hiâŸ© := Int.sq_of_gcd_eq_one hcp hs.symm
  -- âŠ¢ False
  -- use m is positive to exclude m = - i ^ 2
  have hi' : Â¬m = -i ^ 2 := by
    by_contra h1
    have hit : -i ^ 2 â‰¤ 0
    apply neg_nonpos.mpr (sq_nonneg i)
    rw [â† h1] at hit
    apply absurd h4 (not_lt.mpr hit)
  replace hi : m = i ^ 2
  -- âŠ¢ m = i ^ 2
  Â· apply Or.resolve_right hi hi'
    -- ğŸ‰ no goals
  rw [mul_comm] at hs
  -- âŠ¢ False
  rw [Int.gcd_comm] at hcp
  -- âŠ¢ False
  -- obtain d such that r * s = d ^ 2
  obtain âŸ¨d, hdâŸ© := Int.sq_of_gcd_eq_one hcp hs.symm
  -- âŠ¢ False
  -- (b / 2) ^ 2 and m are positive so r * s is positive
  have hd' : Â¬r * s = -d ^ 2 := by
    by_contra h1
    rw [h1] at hs
    have h2 : b' ^ 2 â‰¤ 0 := by
      rw [hs, (by ring : -d ^ 2 * m = -(d ^ 2 * m))]
      exact neg_nonpos.mpr ((zero_le_mul_right h4).mpr (sq_nonneg d))
    have h2' : 0 â‰¤ b' ^ 2 := by apply sq_nonneg b'
    exact absurd (lt_of_le_of_ne h2' (Ne.symm (pow_ne_zero _ h2b0))) (not_lt.mpr h2)
  replace hd : r * s = d ^ 2
  -- âŠ¢ r * s = d ^ 2
  Â· apply Or.resolve_right hd hd'
    -- ğŸ‰ no goals
  -- r = +/- j ^ 2
  obtain âŸ¨j, hjâŸ© := Int.sq_of_gcd_eq_one htt4 hd
  -- âŠ¢ False
  have hj0 : j â‰  0 := by
    intro h0
    rw [h0, zero_pow zero_lt_two, neg_zero, or_self_iff] at hj
    apply left_ne_zero_of_mul hrsz hj
  rw [mul_comm] at hd
  -- âŠ¢ False
  rw [Int.gcd_comm] at htt4
  -- âŠ¢ False
  -- s = +/- k ^ 2
  obtain âŸ¨k, hkâŸ© := Int.sq_of_gcd_eq_one htt4 hd
  -- âŠ¢ False
  have hk0 : k â‰  0 := by
    intro h0
    rw [h0, zero_pow zero_lt_two, neg_zero, or_self_iff] at hk
    apply right_ne_zero_of_mul hrsz hk
  have hj2 : r ^ 2 = j ^ 4 := by
    cases' hj with hjp hjp <;>
      Â· rw [hjp]
        ring
  have hk2 : s ^ 2 = k ^ 4 := by
    cases' hk with hkp hkp <;>
      Â· rw [hkp]
        ring
  -- from m = r ^ 2 + s ^ 2 we now get a new solution to a ^ 4 + b ^ 4 = c ^ 2:
  have hh : i ^ 2 = j ^ 4 + k ^ 4 := by rw [â† hi, htt3, hj2, hk2]
  -- âŠ¢ False
  have hn : n â‰  0 := by
    rw [ht2] at hb20
    apply right_ne_zero_of_mul hb20
  -- and it has a smaller c: from c = m ^ 2 + n ^ 2 we see that m is smaller than c, and i ^ 2 = m.
  have hic : Int.natAbs i < Int.natAbs c := by
    apply Int.ofNat_lt.mp
    rw [â† Int.eq_natAbs_of_zero_le (le_of_lt hc)]
    apply gt_of_gt_of_ge _ (Int.natAbs_le_self_sq i)
    rw [â† hi, ht3]
    apply gt_of_gt_of_ge _ (Int.le_self_sq m)
    exact lt_add_of_pos_right (m ^ 2) (sq_pos_of_ne_zero n hn)
  have hic' : Int.natAbs c â‰¤ Int.natAbs i := by
    apply h.2 j k i
    exact âŸ¨hj0, hk0, hh.symmâŸ©
  apply absurd (not_le_of_lt hic) (not_not.mpr hic')
  -- ğŸ‰ no goals
#align fermat_42.not_minimal Fermat42.not_minimal

end Fermat42

theorem not_fermat_42 {a b c : â„¤} (ha : a â‰  0) (hb : b â‰  0) : a ^ 4 + b ^ 4 â‰  c ^ 2 := by
  intro h
  -- âŠ¢ False
  obtain âŸ¨a0, b0, c0, âŸ¨hf, h2, hpâŸ©âŸ© :=
    Fermat42.exists_pos_odd_minimal (And.intro ha (And.intro hb h))
  apply Fermat42.not_minimal hf h2 hp
  -- ğŸ‰ no goals
#align not_fermat_42 not_fermat_42

theorem not_fermat_4 {a b c : â„¤} (ha : a â‰  0) (hb : b â‰  0) : a ^ 4 + b ^ 4 â‰  c ^ 4 := by
  intro heq
  -- âŠ¢ False
  apply @not_fermat_42 _ _ (c ^ 2) ha hb
  -- âŠ¢ a ^ 4 + b ^ 4 = (c ^ 2) ^ 2
  rw [heq]; ring
  -- âŠ¢ c ^ 4 = (c ^ 2) ^ 2
            -- ğŸ‰ no goals
#align not_fermat_4 not_fermat_4
