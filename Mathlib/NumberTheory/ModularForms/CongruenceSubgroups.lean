/-
Copyright (c) 2022 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/
import Mathlib.LinearAlgebra.Matrix.Integer
import Mathlib.NumberTheory.ModularForms.ArithmeticSubgroups

/-!
# Congruence subgroups

This defines congruence subgroups of `SL(2, ‚Ñ§)` such as `Œì(N)`, `Œì‚ÇÄ(N)` and `Œì‚ÇÅ(N)` for `N` a
natural number.

It also contains basic results about congruence subgroups.

-/

open Matrix.SpecialLinearGroup Matrix

open scoped MatrixGroups ModularGroup Real

variable (N : ‚Ñï)

local notation "SLMOD(" N ")" =>
  @Matrix.SpecialLinearGroup.map (Fin 2) _ _ _ _ _ _ (Int.castRingHom (ZMod N))

@[simp]
theorem SL_reduction_mod_hom_val (Œ≥ : SL(2, ‚Ñ§)) (i j : Fin 2) :
    SLMOD(N) Œ≥ i j = (Œ≥ i j : ZMod N) :=
  rfl

namespace CongruenceSubgroup

/-- The full level `N` congruence subgroup of `SL(2, ‚Ñ§)` of matrices that reduce to the identity
modulo `N`. -/
def Gamma : Subgroup SL(2, ‚Ñ§) :=
  SLMOD(N).ker

@[inherit_doc] scoped notation "Œì(" n ")" => Gamma n

theorem Gamma_mem' {N} {Œ≥ : SL(2, ‚Ñ§)} : Œ≥ ‚àà Gamma N ‚Üî SLMOD(N) Œ≥ = 1 :=
  Iff.rfl

@[simp]
theorem Gamma_mem {N} {Œ≥ : SL(2, ‚Ñ§)} : Œ≥ ‚àà Gamma N ‚Üî (Œ≥ 0 0 : ZMod N) = 1 ‚àß
    (Œ≥ 0 1 : ZMod N) = 0 ‚àß (Œ≥ 1 0 : ZMod N) = 0 ‚àß (Œ≥ 1 1 : ZMod N) = 1 := by
  rw [Gamma_mem']
  constructor
  ¬∑ intro h
    simp [‚Üê SL_reduction_mod_hom_val N Œ≥, h]
  ¬∑ intro h
    ext i j
    rw [SL_reduction_mod_hom_val N Œ≥]
    fin_cases i <;> fin_cases j <;> simp only
    exacts [h.1, h.2.1, h.2.2.1, h.2.2.2]

theorem Gamma_normal : Subgroup.Normal (Gamma N) :=
  SLMOD(N).normal_ker

theorem Gamma_one_top : Gamma 1 = ‚ä§ := by
  ext
  simp [eq_iff_true_of_subsingleton]

lemma mem_Gamma_one (Œ≥ : SL(2, ‚Ñ§)) : Œ≥ ‚àà Œì(1) := by
  simp only [Gamma_one_top, Subgroup.mem_top]

theorem Gamma_zero_bot : Gamma 0 = ‚ä• := rfl

lemma ModularGroup_T_pow_mem_Gamma (N M : ‚Ñ§) (hNM : N ‚à£ M) :
    (ModularGroup.T ^ M) ‚àà Gamma (Int.natAbs N) := by
  simp only [Gamma_mem, Fin.isValue, ModularGroup.coe_T_zpow, of_apply, cons_val', cons_val_zero,
    empty_val', cons_val_fin_one, Int.cast_one, cons_val_one,
    Int.cast_zero, and_self, and_true, true_and]
  refine Iff.mpr (ZMod.intCast_zmod_eq_zero_iff_dvd M (Int.natAbs N)) ?_
  simp only [Int.natCast_natAbs, abs_dvd, hNM]

instance instFiniteIndexGamma [NeZero N] : (Gamma N).FiniteIndex := Subgroup.finiteIndex_ker _

/-- The congruence subgroup of `SL(2, ‚Ñ§)` of matrices whose lower left-hand entry reduces to zero
modulo `N`. -/
def Gamma0 : Subgroup SL(2, ‚Ñ§) where
  carrier := { g | (g 1 0 : ZMod N) = 0 }
  one_mem' := by simp
  mul_mem' := by
    intro a b ha hb
    simp only [Set.mem_setOf_eq]
    have h := (Matrix.two_mul_expl a.1 b.1).2.2.1
    simp only [coe_mul, Set.mem_setOf_eq] at *
    rw [h]
    simp [ha, hb]
  inv_mem' := by
    intro a ha
    simp only [Set.mem_setOf_eq]
    rw [SL2_inv_expl a]
    simp only [cons_val_zero, cons_val_one,
      Int.cast_neg, neg_eq_zero, Set.mem_setOf_eq] at *
    exact ha

@[simp]
theorem Gamma0_mem {N} {A : SL(2, ‚Ñ§)} : A ‚àà Gamma0 N ‚Üî (A 1 0 : ZMod N) = 0 :=
  Iff.rfl

/-- The group homomorphism from `CongruenceSubgroup.Gamma0` to `ZMod N` given by
mapping a matrix to its lower right-hand entry. -/
def Gamma0Map (N : ‚Ñï) : Gamma0 N ‚Üí* ZMod N where
  toFun g := g.1 1 1
  map_one' := by simp
  map_mul' := by
    rintro ‚ü®A, hA‚ü© ‚ü®B, _‚ü©
    simp only [MulMemClass.mk_mul_mk, Fin.isValue, coe_mul, (two_mul_expl A.1 B).2.2.2,
      Int.cast_add, Int.cast_mul, Gamma0_mem.mp hA, zero_mul, zero_add]

/-- The congruence subgroup `Gamma1` (as a subgroup of `Gamma0`) of matrices whose bottom
row is congruent to `(0, 1)` modulo `N`. -/
def Gamma1' (N : ‚Ñï) : Subgroup (Gamma0 N) :=
  (Gamma0Map N).ker

@[simp]
theorem Gamma1_mem' {N} {Œ≥ : Gamma0 N} : Œ≥ ‚àà Gamma1' N ‚Üî Gamma0Map N Œ≥ = 1 :=
  Iff.rfl

theorem Gamma1_to_Gamma0_mem {N} (A : Gamma0 N) :
    A ‚àà Gamma1' N ‚Üî
    ((A.1 0 0 : ‚Ñ§) : ZMod N) = 1 ‚àß ((A.1 1 1 : ‚Ñ§) : ZMod N) = 1
      ‚àß ((A.1 1 0 : ‚Ñ§) : ZMod N) = 0 := by
  constructor
  ¬∑ intro ha
    have adet : (A.1.1.det : ZMod N) = 1 := by simp only [A.1.property, Int.cast_one]
    rw [Matrix.det_fin_two] at adet
    simp only [Gamma1_mem', Gamma0Map, MonoidHom.coe_mk, OneHom.coe_mk, Int.cast_sub,
      Int.cast_mul] at *
    simpa only [Gamma1_mem', Gamma0Map, MonoidHom.coe_mk, OneHom.coe_mk, Int.cast_sub,
      Int.cast_mul, ha, Gamma0_mem.mp A.property, and_self_iff, and_true, mul_one, mul_zero,
      sub_zero] using adet
  ¬∑ intro ha
    simp only [Gamma1_mem', Gamma0Map, MonoidHom.coe_mk]
    exact ha.2.1

/-- The congruence subgroup `Gamma1` of `SL(2, ‚Ñ§)` consisting of matrices
whose bottom row is congruent to `(0,1)` modulo `N`. -/
def Gamma1 (N : ‚Ñï) : Subgroup SL(2, ‚Ñ§) :=
  Subgroup.map ((Gamma0 N).subtype.comp (Gamma1' N).subtype) ‚ä§

@[simp]
theorem Gamma1_mem (N : ‚Ñï) (A : SL(2, ‚Ñ§)) : A ‚àà Gamma1 N ‚Üî
    (A 0 0 : ZMod N) = 1 ‚àß (A 1 1 : ZMod N) = 1 ‚àß (A 1 0 : ZMod N) = 0 := by
  constructor
  ¬∑ intro ha
    simp_rw [Gamma1, Subgroup.mem_map] at ha
    obtain ‚ü®‚ü®x, hx‚ü©, hxx‚ü© := ha
    rw [Gamma1_to_Gamma0_mem] at hx
    simp only [Subgroup.mem_top, true_and] at hxx
    rw [‚Üê hxx]
    convert hx
  ¬∑ intro ha
    simp_rw [Gamma1, Subgroup.mem_map]
    have hA : A ‚àà Gamma0 N := by simp [ha.right.right, Gamma0_mem]
    have HA : (‚ü®A, hA‚ü© : Gamma0 N) ‚àà Gamma1' N := by
      simp only [Gamma1_to_Gamma0_mem]
      exact ha
    refine ‚ü®(‚ü®(‚ü®A, hA‚ü© : Gamma0 N), HA‚ü© : (Gamma1' N : Subgroup (Gamma0 N))), ?_‚ü©
    simp

theorem Gamma1_in_Gamma0 (N : ‚Ñï) : Gamma1 N ‚â§ Gamma0 N := by
  intro x HA
  simp only [Gamma0_mem, Gamma1_mem] at *
  exact HA.2.2

section CongruenceSubgroups

/-- A congruence subgroup is a subgroup of `SL(2, ‚Ñ§)` which contains some `Gamma N` for some
`N ‚â† 0`. -/
def IsCongruenceSubgroup (Œì : Subgroup SL(2, ‚Ñ§)) : Prop :=
  ‚àÉ N ‚â† 0, Gamma N ‚â§ Œì

theorem isCongruenceSubgroup_trans (H K : Subgroup SL(2, ‚Ñ§)) (h : H ‚â§ K)
    (h2 : IsCongruenceSubgroup H) : IsCongruenceSubgroup K := by
  obtain ‚ü®N, hN‚ü© := h2
  exact ‚ü®N, hN.1, hN.2.trans h‚ü©

theorem Gamma_is_cong_sub (N : ‚Ñï) [NeZero N] : IsCongruenceSubgroup (Gamma N) :=
  ‚ü®N, NeZero.ne _, le_rfl‚ü©

theorem Gamma1_is_congruence (N : ‚Ñï) [NeZero N] : IsCongruenceSubgroup (Gamma1 N) := by
  refine ‚ü®N, NeZero.ne _, fun A hA ‚Ü¶ ?_‚ü©
  simp_all [Gamma1_mem, Gamma_mem]

theorem Gamma0_is_congruence (N : ‚Ñï) [NeZero N] : IsCongruenceSubgroup (Gamma0 N) :=
  isCongruenceSubgroup_trans _ _ (Gamma1_in_Gamma0 N) (Gamma1_is_congruence N)

lemma IsCongruenceSubgroup.finiteIndex {Œì : Subgroup SL(2, ‚Ñ§)}
    (h : IsCongruenceSubgroup Œì) : Œì.FiniteIndex := by
  obtain ‚ü®N, hN‚ü© := h
  have : NeZero N := ‚ü®hN.1‚ü©
  exact Subgroup.finiteIndex_of_le hN.2

instance instFiniteIndexGamma0 [NeZero N] : (Gamma0 N).FiniteIndex :=
  (Gamma0_is_congruence N).finiteIndex

instance instFiniteIndexGamma1 [NeZero N] : (Gamma1 N).FiniteIndex :=
  (Gamma1_is_congruence N).finiteIndex

end CongruenceSubgroups

section Conjugation

open Pointwise ConjAct

/-- The subgroup `SL(2, ‚Ñ§) ‚à© g‚Åª¬π Œì g`, for `Œì` a subgroup of `SL(2, ‚Ñ§)` and `g ‚àà GL(2, ‚Ñù)`. -/
def conjGL (Œì : Subgroup SL(2, ‚Ñ§)) (g : GL (Fin 2) ‚Ñù) : Subgroup SL(2, ‚Ñ§) :=
  ((toConjAct g‚Åª¬π) ‚Ä¢ (Œì.map <| mapGL ‚Ñù)).comap (mapGL ‚Ñù)

@[simp] lemma mem_conjGL {Œì : Subgroup SL(2, ‚Ñ§)} {g : GL (Fin 2) ‚Ñù} {x : SL(2, ‚Ñ§)} :
    x ‚àà conjGL Œì g ‚Üî ‚àÉ y ‚àà Œì, y = g * x * g‚Åª¬π := by
  simp [conjGL, mapGL, Subgroup.mem_inv_pointwise_smul_iff, toConjAct_smul]

@[deprecated "use mem_conjGL" (since := "2025-08-16")]
lemma mem_conjGL' {Œì : Subgroup SL(2, ‚Ñ§)} {g : GL (Fin 2) ‚Ñù} {x : SL(2, ‚Ñ§)} :
    x ‚àà conjGL Œì g ‚Üî ‚àÉ y ‚àà Œì, g‚Åª¬π * y * g = x := by
  rw [mem_conjGL]
  refine exists_congr fun y ‚Ü¶ and_congr_right fun hy ‚Ü¶ ?_
  rw [eq_mul_inv_iff_mul_eq, mul_assoc, inv_mul_eq_iff_eq_mul]

@[simp]
lemma conjGL_coe (Œì : Subgroup SL(2, ‚Ñ§)) (g : SL(2, ‚Ñ§)) :
    conjGL Œì g = (toConjAct g‚Åª¬π) ‚Ä¢ Œì := by
  ext x
  simp_rw [mem_conjGL, ‚Üê map_inv, ‚Üê map_mul, toGL_injective.eq_iff, map_intCast_injective.eq_iff,
    exists_eq_right, toConjAct_inv, Subgroup.mem_inv_pointwise_smul_iff, toConjAct_smul]

@[deprecated (since := "2025-05-15")] alias conjGLPos := conjGL
@[deprecated (since := "2025-05-15")] alias conjGLPos_coe := conjGL_coe
@[deprecated (since := "2025-05-15")] alias mem_conjGLPos := mem_conjGL
@[deprecated (since := "2025-05-15")] alias mem_conjGLPos' := mem_conjGL'

theorem Gamma_cong_eq_self (N : ‚Ñï) (g : ConjAct SL(2, ‚Ñ§)) : g ‚Ä¢ Gamma N = Gamma N := by
  apply Subgroup.Normal.conjAct (Gamma_normal N)

theorem conj_cong_is_cong (g : ConjAct SL(2, ‚Ñ§)) (Œì : Subgroup SL(2, ‚Ñ§))
    (h : IsCongruenceSubgroup Œì) : IsCongruenceSubgroup (g ‚Ä¢ Œì) := by
  obtain ‚ü®N, HN‚ü© := h
  refine ‚ü®N, ?_‚ü©
  rw [‚Üê Gamma_cong_eq_self N g, Subgroup.pointwise_smul_le_pointwise_smul_iff]
  exact HN

/-- For any `g ‚àà GL(2, ‚Ñö)` and `M ‚â† 0`, there exists `N` such that `g x g‚Åª¬π ‚àà Œì(M)` for all
`x ‚àà Œì(N)`. -/
theorem exists_Gamma_le_conj (g : GL (Fin 2) ‚Ñö) (M : ‚Ñï) [NeZero M] :
    ‚àÉ N ‚â† 0, ‚àÄ x ‚àà Gamma N, g * (mapGL ‚Ñö x) * g‚Åª¬π ‚àà (Gamma M).map (mapGL ‚Ñö) := by
  -- Give names to the numerators and denominators of `g` and `g‚Åª¬π`
  let A‚ÇÅ := g.1
  let A‚ÇÇ := (g‚Åª¬π).1
  have hA‚ÇÅ‚ÇÇ : A‚ÇÅ * A‚ÇÇ = 1 := by simp only [‚Üê Matrix.GeneralLinearGroup.coe_mul,
    mul_inv_cancel, Matrix.GeneralLinearGroup.coe_one, A‚ÇÅ, A‚ÇÇ]
  let a‚ÇÅ := A‚ÇÅ.den
  let a‚ÇÇ := A‚ÇÇ.den
  -- we take `N = a‚ÇÅ * a‚ÇÇ`
  refine ‚ü®a‚ÇÅ * a‚ÇÇ * M, mul_ne_zero (mul_ne_zero A‚ÇÅ.den_ne_zero A‚ÇÇ.den_ne_zero) (NeZero.ne _),
    fun ‚ü®y, hy‚ü© hy' ‚Ü¶ ?_‚ü©
  -- Show that `y` is of the form `1 + (a‚ÇÅ * a‚ÇÇ) ‚Ä¢ k` for some integer matrix `k`.
  obtain ‚ü®k, hk‚ü© : ‚àÉ k, y = 1 + (a‚ÇÅ * a‚ÇÇ * M) ‚Ä¢ k := by
    replace hy' : y.map (Int.cast : ‚Ñ§ ‚Üí ZMod (a‚ÇÅ * a‚ÇÇ * M)) = 1 := by
      rw [CongruenceSubgroup.Gamma_mem', Subtype.ext_iff] at hy'
      simpa using hy'
    use Matrix.of fun i j ‚Ü¶ (y - 1) i j / (a‚ÇÅ * a‚ÇÇ * M)
    rw [‚Üê sub_eq_iff_eq_add']
    ext i j
    simp_rw [Matrix.smul_apply, Matrix.of_apply, nsmul_eq_mul, Nat.cast_mul]
    refine (Int.mul_ediv_cancel_of_dvd ?_).symm
    rw [‚Üê Matrix.map_one Int.cast (by simp) (by simp), ‚Üê sub_eq_zero,
      ‚Üê Matrix.map_sub _ (by simp)] at hy'
    simpa only [Matrix.zero_apply, Matrix.map_apply, ZMod.intCast_zmod_eq_zero_iff_dvd,
      Nat.cast_mul] using congr_fun‚ÇÇ hy' i j
  -- use this `k` to cook up a new integer matrix, which we will show comes from `SL(2, ‚Ñ§)`
  let z := 1 + M ‚Ä¢ (A‚ÇÅ.num * k * A‚ÇÇ.num)
  have hz_coe : z.map Int.cast = A‚ÇÅ * (y.map Int.cast) * A‚ÇÇ := by
    simp only [Matrix.map_add _ Int.cast_add, Matrix.map_one _ Int.cast_zero Int.cast_one, hk,
      mul_add, mul_one, add_mul, hA‚ÇÅ‚ÇÇ, add_right_inj, z]
    conv_rhs => rw [‚Üê A‚ÇÅ.inv_denom_smul_num, ‚Üê A‚ÇÇ.inv_denom_smul_num, Matrix.map_smul _ _ (by simp)]
    simp only [Matrix.smul_mul, Matrix.mul_smul, Matrix.map_smul (Int.cast : ‚Ñ§ ‚Üí ‚Ñö) M (by simp),
      Matrix.map_mul_intCast]
    rw [‚Üê Nat.cast_smul_eq_nsmul ‚Ñö (_ * M), ‚Üê MulAction.mul_smul, ‚Üê MulAction.mul_smul,
      mul_comm a‚ÇÅ a‚ÇÇ, Nat.cast_mul, Nat.cast_mul, mul_assoc _ _ (M : ‚Ñö), mul_comm _ (M : ‚Ñö),
      inv_mul_cancel_left‚ÇÄ (mod_cast A‚ÇÇ.den_ne_zero),
      mul_inv_cancel_right‚ÇÄ (mod_cast A‚ÇÅ.den_ne_zero), Nat.cast_smul_eq_nsmul]
  have hz_det : z.det = 1 := by
    have := congr_arg Matrix.det hz_coe
    simp_rw [Matrix.det_mul, ‚Üê Int.cast_det] at this
    rwa [mul_right_comm, ‚Üê Matrix.det_mul, hA‚ÇÅ‚ÇÇ, Matrix.det_one, one_mul, hy, Int.cast_inj] at this
  refine ‚ü®‚ü®z, hz_det‚ü©, ?_, by simpa only [Subtype.ext_iff, Subgroup.coe_mul, Units.ext_iff,
    Units.val_mul] using hz_coe‚ü©
  rw [SetLike.mem_coe, CongruenceSubgroup.Gamma_mem', Subtype.ext_iff]
  ext i j
  simp_rw [map_apply_coe, z, map_add, map_one, RingHom.mapMatrix_apply, Int.coe_castRingHom,
    add_apply, map_apply, coe_one, add_eq_left, Matrix.smul_apply, nsmul_eq_mul, Int.cast_mul,
    Int.cast_natCast, ZMod.natCast_self M, zero_mul]

/-- For any `g ‚àà GL(2, ‚Ñö)` and `M ‚â† 0`, there exists `N` such that `g Œì(N) g‚Åª¬π ‚â§ Œì(M)`. -/
theorem exists_Gamma_le_conj' (g : GL (Fin 2) ‚Ñö) (M : ‚Ñï) [NeZero M] :
    ‚àÉ N ‚â† 0, (toConjAct <| g.map (Rat.castHom ‚Ñù)) ‚Ä¢ (Gamma N).map (mapGL ‚Ñù)
      ‚â§ (Gamma M).map (mapGL ‚Ñù) := by
  obtain ‚ü®N, hN, h‚ü© := exists_Gamma_le_conj g M
  refine ‚ü®N, hN, fun y hy ‚Ü¶ ?_‚ü©
  simp_rw [Subgroup.mem_pointwise_smul_iff_inv_smul_mem, Subgroup.mem_map,
    eq_inv_smul_iff] at hy
  obtain ‚ü®x, hx, rfl‚ü© := hy
  obtain ‚ü®z, hz, hz'‚ü© := h x hx
  use z, hz
  simpa only [Subtype.ext_iff, Units.ext_iff, map_mul] using
    congr_arg (GeneralLinearGroup.map (Rat.castHom ‚Ñù)) hz'

open Subgroup in
/-- If `Œì` has finite index in `SL(2, ‚Ñ§)`, then so does `g‚Åª¬π Œì g ‚à© SL(2, ‚Ñ§)` for any
`g ‚àà GL(2, ‚Ñö)`. -/
lemma finiteIndex_conjGL (g : GL (Fin 2) ‚Ñö) : (conjGL ‚ä§ (g.map <| Rat.castHom ‚Ñù)).FiniteIndex := by
  constructor
  let t := (toConjAct <| g.map <| Rat.castHom ‚Ñù)‚Åª¬π
  suffices (t ‚Ä¢ ùíÆ‚Ñí ‚äì ùíÆ‚Ñí).relIndex ùíÆ‚Ñí ‚â† 0 by
    rwa [conjGL, index_comap, ‚Üê inf_relIndex_right, ‚Üê MonoidHom.range_eq_map]
  obtain ‚ü®N, hN, hN'‚ü© := exists_Gamma_le_conj' g 1
  rw [Gamma_one_top, ‚Üê MonoidHom.range_eq_map] at hN'
  suffices Œì(N) ‚â§ (t ‚Ä¢ ùíÆ‚Ñí ‚äì ùíÆ‚Ñí).comap (mapGL ‚Ñù) by
    haveI _ : NeZero N := ‚ü®hN‚ü©
    simpa only [index_comap] using (finiteIndex_of_le this).index_ne_zero
  intro k hk
  simpa [mem_pointwise_smul_iff_inv_smul_mem] using
    hN' <| smul_mem_pointwise_smul _ _ _ ‚ü®k, hk, rfl‚ü©

/-- Conjugates of `SL(2, ‚Ñ§)` by `GL(2, ‚Ñö)` are arithmetic subgroups. -/
lemma isArithmetic_conj_SL2Z (g : GL (Fin 2) ‚Ñö) :
    (toConjAct (g.map (Rat.castHom ‚Ñù)) ‚Ä¢ ùíÆ‚Ñí).IsArithmetic := by
  constructor
  rw [MonoidHom.range_eq_map]
  constructor
  ¬∑ rw [‚Üê Subgroup.relIndex_comap, Subgroup.relIndex_top_right]
    exact (finiteIndex_conjGL g‚Åª¬π).index_ne_zero
  ¬∑ rw [‚Üê Subgroup.relIndex_pointwise_smul (toConjAct (g.map (Rat.castHom ‚Ñù)))‚Åª¬π,
      inv_smul_smul, ‚Üê Subgroup.relIndex_comap, Subgroup.relIndex_top_right]
    exact (finiteIndex_conjGL g).index_ne_zero

/-- Conjugation by `GL(2, ‚Ñö)` preserves arithmetic subgroups. -/
lemma _root_.Subgroup.IsArithmetic.conj (ùí¢ : Subgroup (GL (Fin 2) ‚Ñù)) [ùí¢.IsArithmetic]
    (g : GL (Fin 2) ‚Ñö) :
    (toConjAct (g.map (Rat.castHom ‚Ñù)) ‚Ä¢ ùí¢).IsArithmetic :=
  ‚ü®(Subgroup.IsArithmetic.is_commensurable.conj _).trans
    (isArithmetic_conj_SL2Z g).is_commensurable‚ü©

@[deprecated (since := "2025-09-17")] alias IsArithmetic.conj := _root_.Subgroup.IsArithmetic.conj

/-- If `Œì` is a congruence subgroup, then so is `g‚Åª¬π Œì g ‚à© SL(2, ‚Ñ§)` for any `g ‚àà GL(2, ‚Ñö)`. -/
lemma IsCongruenceSubgroup.conjGL {Œì : Subgroup SL(2, ‚Ñ§)} (hŒì : IsCongruenceSubgroup Œì)
    (g : GL (Fin 2) ‚Ñö) :
    IsCongruenceSubgroup (conjGL Œì (g.map <| Rat.castHom ‚Ñù)) := by
  obtain ‚ü®M, hN, hŒìM‚ü© := hŒì
  haveI _ : NeZero M := ‚ü®hN‚ü©
  obtain ‚ü®N, hN, hN'‚ü© := exists_Gamma_le_conj' g M
  rw [Subgroup.pointwise_smul_subset_iff] at hN'
  refine ‚ü®N, ‚Äπ_‚Ä∫, fun x hx ‚Ü¶ ?_‚ü©
  obtain ‚ü®y, hy, hy'‚ü© := Subgroup.mem_inv_pointwise_smul_iff.mp <| hN' ‚ü®x, hx, rfl‚ü©
  exact mem_conjGL.mpr ‚ü®y, hŒìM hy, hy'‚ü©

end Conjugation

end CongruenceSubgroup
