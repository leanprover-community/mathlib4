/-
Copyright (c) 2024 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/

import Mathlib.NumberTheory.ModularForms.EisensteinSeries.UniformConvergence
import Mathlib.Analysis.Complex.UpperHalfPlane.Manifold
import Mathlib.Analysis.Complex.LocallyUniformLimit
import Mathlib.Analysis.Complex.HalfPlane

/-!
# Holomorphicity of Eisenstein series

We show that Eisenstein series of weight `k` and level `Î“(N)` with congruence condition
`a : Fin 2 â†’ ZMod N` are holomorphic on the upper half plane.
-/

noncomputable section

open ModularForm EisensteinSeries UpperHalfPlane Set Filter Function Complex Manifold

open scoped Topology BigOperators Nat Classical

namespace EisensteinSeries

/-- Extend a function on `â„` arbitrarily to a function on all of `â„‚`. -/
local notation "â†‘â‚•" f => f âˆ˜ (PartialHomeomorph.symm
          (OpenEmbedding.toPartialHomeomorph UpperHalfPlane.coe openEmbedding_coe))

/--Auxilary lemma showing that for any `k : â„¤` the function `z â†’ 1/(c*z+d)^k` is
differentiable on `{z : â„‚ | 0 < z.im}`. -/
lemma div_linear_zpow_differentiableOn (k : â„¤) (a : Fin 2 â†’ â„¤) :
    DifferentiableOn â„‚ (fun z : â„‚ => 1 / (a 0 * z + a 1) ^ k) {z : â„‚ | 0 < z.im} := by
  by_cases ha : a â‰  0
  Â· apply DifferentiableOn.div (differentiableOn_const 1)
    Â· apply DifferentiableOn.zpow
      fun_prop
      left
      exact fun z hz â†¦ linear_ne_zero ((Int.cast (R := â„)) âˆ˜ a) âŸ¨z, hzâŸ©
        ((Function.comp_ne_zero_iff _ Int.cast_injective Int.cast_zero ).mpr ha)
    Â· exact fun z hz â†¦ zpow_ne_zero k (linear_ne_zero ((Int.cast (R := â„)) âˆ˜ a)
        âŸ¨z, hzâŸ© ((Function.comp_ne_zero_iff _ Int.cast_injective Int.cast_zero ).mpr ha))
  Â· rw [ne_eq, not_not] at ha
    simp only [ha, Fin.isValue, Pi.zero_apply, Int.cast_zero, zero_mul, add_zero, one_div]
    exact differentiableOn_const (0 ^ k)â»Â¹

/--Auxilary lemma showing that for any `k : â„¤` and `(a : Fin 2 â†’ â„¤)`
the function `eisSummand` is differentiable on `{z : â„‚ | 0 < z.im}`.-/
lemma eisSummad_extension_differentiableOn (k : â„¤) (a : Fin 2 â†’ â„¤) :
    DifferentiableOn â„‚ (â†‘â‚•eisSummand k a) {z : â„‚ | 0 < z.im} := by
  apply DifferentiableOn.congr (div_linear_zpow_differentiableOn k a)
  intro z hz
  rw [â† coe_image_eq] at hz
  have := PartialHomeomorph.left_inv (PartialHomeomorph.symm
    (OpenEmbedding.toPartialHomeomorph UpperHalfPlane.coe openEmbedding_coe)) hz
  simp only [PartialHomeomorph.symm_symm, OpenEmbedding.toPartialHomeomorph_apply,
    UpperHalfPlane.coe] at this
  simp only [comp_apply, eisSummand, Fin.isValue, this, one_div]

/--Eisenstein series are MDifferentiable (i.e. holomorphic functions from `â„ â†’ â„‚`). -/
theorem eisensteinSeries_SIF_MDifferentiable {k : â„¤} {N : â„•} (hk : 3 â‰¤ k) (a : Fin 2 â†’ ZMod N) :
    MDifferentiable ð“˜(â„‚) ð“˜(â„‚) (eisensteinSeries_SIF a k).toFun := by
  rw [MDifferentiable_iff_extension_DifferentiableOn, coe_image_eq]
  apply @TendstoLocallyUniformlyOn.differentiableOn (E := â„‚) (Î¹ := (Finset â†‘(gammaSet N a))) _ _ _
    (U := {z : â„‚ | 0 < z.im}) atTop (fun (s : Finset (gammaSet N a)) =>
      â†‘â‚•(fun (z : â„) => âˆ‘ x in s, eisSummand k x z )) (â†‘â‚•((eisensteinSeries_SIF a k).toFun ))
        (atTop_neBot) (eisensteinSeries_tendstoLocallyUniformlyOn hk a)
          ((eventually_of_forall fun s =>
            DifferentiableOn.sum fun s _ â†¦ eisSummad_extension_differentiableOn _ _)) ?_
  simpa only [EReal.coe_pos] using Complex.isOpen_im_gt_EReal 0
