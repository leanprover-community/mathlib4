/-
Copyright (c) 2024 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/

import Mathlib.NumberTheory.ModularForms.EisensteinSeries.UniformConvergence
import Mathlib.Analysis.Complex.UpperHalfPlane.Manifold
import Mathlib.Analysis.Complex.LocallyUniformLimit
import Mathlib.Geometry.Manifold.MFDeriv.FDeriv

/-!
# Holomorphicity of Eisenstein series

We show that Eisenstein series of weight `k` and level `Î“(N)` with congruence condition
`a : Fin 2 â†’ ZMod N` are holomorphic on the upper half plane, which is stated as being
MDifferentiable.
-/

noncomputable section

open ModularForm EisensteinSeries UpperHalfPlane Set Filter Function Complex Manifold

open scoped Topology BigOperators Nat Classical UpperHalfPlane

namespace EisensteinSeries

/-- Auxilary lemma showing that for any `k : â„¤` the function `z â†’ 1/(c*z+d)^k` is
differentiable on `{z : â„‚ | 0 < z.im}`. -/
lemma div_linear_zpow_differentiableOn (k : â„¤) (a : Fin 2 â†’ â„¤) :
    DifferentiableOn â„‚ (fun z : â„‚ => 1 / (a 0 * z + a 1) ^ k) {z : â„‚ | 0 < z.im} := by
  rcases ne_or_eq a 0 with ha | rfl
  Â· apply DifferentiableOn.div (differentiableOn_const 1)
    Â· apply DifferentiableOn.zpow
      Â· fun_prop
      Â· left
        exact fun z hz â†¦ linear_ne_zero _ âŸ¨z, hzâŸ©
          ((comp_ne_zero_iff _ Int.cast_injective Int.cast_zero).mpr ha)
    Â·  exact fun z hz â†¦ zpow_ne_zero k (linear_ne_zero (a Â·)
        âŸ¨z, hzâŸ© ((comp_ne_zero_iff _ Int.cast_injective Int.cast_zero).mpr ha))
  Â· simp only [ Fin.isValue, Pi.zero_apply, Int.cast_zero, zero_mul, add_zero, one_div]
    apply differentiableOn_const

/-- Auxilary lemma showing that for any `k : â„¤` and `(a : Fin 2 â†’ â„¤)`
the extension of `eisSummand` is differentiable on `{z : â„‚ | 0 < z.im}`.-/
lemma eisSummand_extension_differentiableOn (k : â„¤) (a : Fin 2 â†’ â„¤) :
    DifferentiableOn â„‚ (â†‘â‚•eisSummand k a) {z : â„‚ | 0 < z.im} := by
  apply DifferentiableOn.congr (div_linear_zpow_differentiableOn k a)
  intro z hz
  lift z to â„ using hz
  apply comp_ofComplex

/-- Eisenstein series are MDifferentiable (i.e. holomorphic functions from `â„ â†’ â„‚`). -/
theorem eisensteinSeries_SIF_MDifferentiable {k : â„¤} {N : â„•} (hk : 3 â‰¤ k) (a : Fin 2 â†’ ZMod N) :
    MDifferentiable ð“˜(â„‚) ð“˜(â„‚) (eisensteinSeries_SIF a k) := by
  intro Ï„
  suffices DifferentiableAt â„‚ (â†‘â‚•eisensteinSeries_SIF a k) Ï„.1 by
    convert MDifferentiableAt.comp Ï„ (DifferentiableAt.mdifferentiableAt this) Ï„.mdifferentiable_coe
    exact funext fun z â†¦ (comp_ofComplex (eisensteinSeries_SIF a k) z).symm
  refine DifferentiableOn.differentiableAt ?_
    ((isOpen_lt continuous_const Complex.continuous_im).mem_nhds Ï„.2)
  exact (eisensteinSeries_tendstoLocallyUniformlyOn hk a).differentiableOn
    (eventually_of_forall fun s â†¦ DifferentiableOn.sum
      fun _ _ â†¦ eisSummand_extension_differentiableOn _ _)
        (isOpen_lt continuous_const continuous_im)
