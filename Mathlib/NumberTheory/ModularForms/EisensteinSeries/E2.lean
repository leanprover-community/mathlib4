/-
Copyright (c) 2025 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/
import Mathlib.Algebra.Order.Ring.Star
import Mathlib.Analysis.CStarAlgebra.Classes
import Mathlib.Data.Int.Star
import Mathlib.NumberTheory.LSeries.RiemannZeta
import Mathlib.NumberTheory.ModularForms.EisensteinSeries.UniformConvergence
import Mathlib.NumberTheory.ModularForms.EisensteinSeries.QExpansion
import Mathlib.Order.Interval.Finset.IccSums

/-!
# Eisenstein Series E2

We define the Eisenstein series `E2` of weight `2` and level `1` as a limit of partial sums
over non-symmetric intervals.

-/

open UpperHalfPlane hiding I

open ModularForm EisensteinSeries  TopologicalSpace  intervalIntegral
  Metric Filter Function Complex MatrixGroups Finset ArithmeticFunction Set

open scoped Interval Real Topology BigOperators Nat

noncomputable section


/-- This is an auxilary summand used to define the Eisenstein serires `G2`. -/
def e2Summand (m : â„¤) (z : â„) : â„‚ := âˆ‘' (n : â„¤), eisSummand 2 ![m, n] z

lemma e2Summand_summable (m : â„¤) (z : â„) : Summable (fun n => eisSummand 2 ![m, n] z) := by
  apply (linear_right_summable z m (k := 2) (by omega)).congr
  simp [eisSummand]

@[simp]
lemma e2Summand_zero_eq_riemannZeta_two (z : â„) : e2Summand 0 z = 2 * riemannZeta 2 := by
  simpa [e2Summand, eisSummand] using
    (two_riemannZeta_eq_tsum_int_inv_even_pow (k := 2) (by omega) (by simp)).symm

theorem e2Summand_even (z : â„) (n : â„¤) : e2Summand n z = e2Summand (-n) z := by
  simp only [e2Summand, â† tsum_int_eq_tsum_neg (fun a => eisSummand 2 ![-n, a] z)]
  congr
  ext b
  simp only [eisSummand, Fin.isValue, Matrix.cons_val_zero, Matrix.cons_val_one,
    Matrix.cons_val_fin_one, Int.reduceNeg, zpow_neg, Int.cast_neg, neg_mul, inv_inj]
  rw_mod_cast [Int.cast_neg]
  ring

/-- The Eisenstein series of weight `2` and level `1` defined as the limit as `N` tends to
infinity of the partial sum of `m` in `[N,N)` of `e2Summand m`. This sum over symmetric
intervals is handy in showing it is Cauchy. -/
def G2 : â„ â†’ â„‚ := fun z => limUnder atTop (fun N : â„• => âˆ‘ m âˆˆ Icc (-N : â„¤) N, e2Summand m z)

/-- The normalised Eisenstein series of weight `2` and level `1`. -/
def E2 : â„ â†’ â„‚ := (1 / (2 * riemannZeta 2)) â€¢  G2

/-- This function measures the defect in `E2` being a modular form. -/
def D2 (Î³ : SL(2, â„¤)) : â„ â†’ â„‚ := fun z => (2 * Ï€ * I * Î³ 1 0) / (denom Î³ z)

lemma G2_partial_sum_eq (z : â„) (N : â„•) : âˆ‘ m âˆˆ Icc (-N : â„¤) N, e2Summand m z =
    (2 * riemannZeta 2) + (âˆ‘ m âˆˆ range N, 2 * (-2 * Ï€ * I) ^ 2  *
    âˆ‘' n : â„•+, n  * cexp (2 * Ï€ * I * (m + 1) * z) ^ (n : â„•)) := by
  rw [sum_Icc_of_even_eq_range (e2Summand_even z), Finset.sum_range_succ', mul_add]
  nth_rw 2 [two_mul]
  ring_nf
  have (a : â„•) := EisensteinSeries.qExpansion_identity_pnat (k := 1) (by omega)
    âŸ¨(a + 1) * z, by simpa [show  0 < a + (1 : â„) by positivity] using z.2âŸ©
  simp only [coe_mk_subtype, add_comm, Nat.reduceAdd, one_div, mul_comm, mul_neg, even_two,
    Even.neg_pow, Nat.factorial_one, Nat.cast_one, div_one, pow_one, e2Summand, eisSummand,
    Nat.cast_add, Fin.isValue, Matrix.cons_val_zero, Int.cast_add, Int.cast_natCast, Int.cast_one,
    Matrix.cons_val_one, Matrix.cons_val_fin_one, Int.reduceNeg, zpow_neg, mul_sum, Int.cast_zero,
    zero_mul, add_zero, I_sq, neg_mul, one_mul] at *
  congr
  Â· simpa using (two_riemannZeta_eq_tsum_int_inv_even_pow (k := 2) (by omega) (by simp)).symm
  Â· ext a
    norm_cast at *
    simp_rw [this a, â† tsum_mul_left, â† tsum_neg,ofReal_mul, ofReal_ofNat, mul_pow, I_sq, neg_mul,
      one_mul, Nat.cast_add, Nat.cast_one, mul_neg, ofReal_pow]
    grind

private lemma aux_tsum_identity (z : â„) : âˆ‘' m : â„•, (2 * (-2 * â†‘Ï€ * I) ^ 2  *
    âˆ‘' n : â„•+, n * cexp (2 * â†‘Ï€ * I * (m + 1) * z) ^ (n : â„•))  =
    -8 * Ï€ ^ 2 * âˆ‘' (n : â„•+), (sigma 1 n) * cexp (2 * Ï€ * I * z) ^ (n : â„•) := by
  have := tsum_prod_pow_cexp_eq_tsum_sigma 1 z
  rw [tsum_pnat_eq_tsum_succ (fun d =>
    âˆ‘' (c : â„•+), (c ^ 1 : â„‚) * cexp (2 * â†‘Ï€ * I * d * z) ^ (c : â„•))] at this
  simp only [neg_mul, even_two, Even.neg_pow, â† tsum_mul_left, â† this, Nat.cast_add, Nat.cast_one]
  apply tsum_congr
  intro b
  apply tsum_congr
  intro c
  simp only [mul_pow, I_sq, mul_neg, mul_one, neg_mul, neg_inj]
  ring

theorem G2_tendsto (z : â„) : Tendsto (fun N â†¦ âˆ‘ x âˆˆ range N, 2 * (2 * â†‘Ï€ * I) ^ 2 *
    âˆ‘' (n : â„•+), n * cexp (2 * â†‘Ï€ * I * (â†‘x + 1) * â†‘z) ^ (n : â„•)) atTop
    (ğ“ (-8 * â†‘Ï€ ^ 2 * âˆ‘' (n : â„•+), â†‘((Ïƒ 1) â†‘n) * cexp (2 * â†‘Ï€ * I * â†‘z) ^ (n : â„•))) := by
  rw [â† aux_tsum_identity]
  have hf : Summable fun m : â„• => ( 2 * (-2 * â†‘Ï€ * I) ^ 2 *
      âˆ‘' n : â„•+, n ^ ((2 - 1)) * Complex.exp (2 * â†‘Ï€ * I * (m + 1) * z) ^ (n : â„•)) := by
    apply Summable.mul_left
    have := (summable_prod_aux 1 z).prod_symm.prod
    have h0 := pnat_summable_iff_summable_succ
      (f := fun b â†¦ âˆ‘' (c : â„•+), c * cexp (2 * â†‘Ï€ * I * â†‘â†‘b * â†‘z) ^ (c : â„•))
    simp at *
    rw [â† h0]
    apply this
  simpa using (hf.hasSum).comp tendsto_finset_range

lemma G2_cauchy (z : â„) : CauchySeq (fun N : â„• â†¦ âˆ‘ m âˆˆ Icc (-N : â„¤) N, e2Summand m z) := by
  conv =>
    enter [1]
    ext n
    rw [G2_partial_sum_eq]
  apply CauchySeq.const_add
  apply Tendsto.cauchySeq (x := -8 * Ï€ ^ 2 * âˆ‘' (n : â„•+), (Ïƒ 1 n) * cexp (2 * Ï€ * I * z) ^ (n : â„•))
  simpa using G2_tendsto z

lemma G2_q_exp (z : â„) : G2 z = (2 * riemannZeta 2) - 8 * Ï€ ^ 2 *
    âˆ‘' n : â„•+, Ïƒ 1 n * cexp (2 * Ï€ * I * z) ^ (n : â„•) := by
  rw [G2, Filter.Tendsto.limUnder_eq, sub_eq_add_neg]
  conv =>
    enter [1]
    ext N
    rw [G2_partial_sum_eq z N]
  apply Filter.Tendsto.add (by simp) (by simpa using G2_tendsto z)

section transform

lemma tendsto_zero_of_cauchySeq_sum_Icc {F : Type*} [NormedRing F] [NormSMulClass â„¤ F] {f : â„¤ â†’ F}
    (hc : CauchySeq fun N : â„• â†¦ âˆ‘ m âˆˆ Icc (-N : â„¤) N, f m) (hs : âˆ€ n , f n = f (-n)) :
    Tendsto f atTop (ğ“ 0) := by
  simp only [cauchySeq_iff_le_tendsto_0, Metric.tendsto_atTop, gt_iff_lt, ge_iff_le,
    dist_zero_right, Real.norm_eq_abs] at *
  obtain âŸ¨g, hg, H, HgâŸ© := hc
  intro Îµ hÎµ
  obtain âŸ¨N, hNâŸ© := (Hg (2 * Îµ) (by positivity))
  refine âŸ¨N + 1, fun n hn => ?_âŸ©
  have H3 := (H n.natAbs (n -1).natAbs N (by omega) (by omega))
  rw [sum_Icc_add_endpoints f (by omega)] at H3
  have h1 : |n| = n := by
    rw [abs_eq_self]
    omega
  have h2 : |n - 1| = n - 1 := by
    rw [abs_eq_self, Int.sub_nonneg]
    omega
  have := norm_smul (2 : â„¤) (f n)
  simp only [Nat.cast_natAbs, h1, Int.cast_eq, â† hs n, (two_mul (f n)).symm, neg_sub, h2,
    Int.cast_sub, Int.cast_one, dist_add_self_left, zsmul_eq_mul, Int.cast_ofNat] at *
  simpa [this, Int.norm_eq_abs] using lt_of_le_of_lt (le_trans H3 (le_abs_self (g N)))
    (hN N (by rfl))

lemma aux_tendsto_Ico (z : â„) : Tendsto (fun (N : â„•) â†¦ âˆ‘ m âˆˆ Ico (-(N : â„¤)) N, e2Summand m z) atTop
    (ğ“ (G2 z)) := by
  apply Tendsto_of_sub_tendsto_zero _ (CauchySeq.tendsto_limUnder (G2_cauchy z))
  have h0 := tendsto_zero_of_cauchySeq_sum_Icc (G2_cauchy z) (by apply e2Summand_even)
  conv =>
    enter [1]
    ext N
    rw [Pi.sub_apply, sum_Icc_eq_sum_Ico_succ _ (by omega), sub_add_cancel_left]
  simpa using  (Filter.Tendsto.neg h0).comp tendsto_natCast_atTop_atTop

lemma G2_Ico (z : â„) : G2 z = limUnder atTop (fun N : â„• â†¦ âˆ‘ m âˆˆ Ico (-N : â„¤) N, e2Summand m z) := by
  apply symm
  rw [G2, Filter.Tendsto.limUnder_eq]
  apply Tendsto_of_sub_tendsto_zero _ (CauchySeq.tendsto_limUnder (G2_cauchy z))
  have h0 := tendsto_zero_of_cauchySeq_sum_Icc (G2_cauchy z) (by apply e2Summand_even)
  conv =>
    enter [1]
    ext N
    rw [Pi.sub_apply, sum_Icc_eq_sum_Ico_succ _ (by omega), sub_add_cancel_left]
  simpa using  (Filter.Tendsto.neg h0).comp tendsto_natCast_atTop_atTop

lemma aux_cauchySeq_Ico (z : â„) :
  CauchySeq fun N : â„• â†¦ âˆ‘ m âˆˆ Finset.Ico (-N : â„¤) N, e2Summand m z := by
    apply Filter.Tendsto.cauchySeq
    apply ((Filter.limUnder_eq_iff (Exists.intro _ (aux_tendsto_Ico z))).mp (G2_Ico z).symm).congr
    simp

lemma limUnder_mul_const {Î± : Type*} [Preorder Î±] [(atTop : Filter Î±).NeBot] (f : Î± â†’ â„‚)
    (hf : CauchySeq f) (c : â„‚) : c * (limUnder atTop f)= limUnder atTop (c â€¢ f) := by
  nth_rw 2 [Filter.Tendsto.limUnder_eq]
  apply Filter.Tendsto.const_mul
  refine CauchySeq.tendsto_limUnder hf

theorem aux_sum_Ico_S_indentity (z : â„) (N : â„•) :
    ((z : â„‚) ^ 2)â»Â¹ * (âˆ‘ x âˆˆ Ico (-N : â„¤) N, âˆ‘' (n : â„¤), (((x : â„‚) * (-â†‘z)â»Â¹ + n) ^ 2)â»Â¹) =
    âˆ‘' (n : â„¤), âˆ‘ x âˆˆ Ico (-N : â„¤) N, (((n : â„‚) * z + x) ^ 2)â»Â¹ := by
  simp_rw [inv_neg, mul_neg]
  rw [Finset.mul_sum, Summable.tsum_finsetSum]
  Â· congr
    ext n
    rw [â† tsum_mul_left, â† tsum_int_eq_tsum_neg]
    apply tsum_congr
    intro d
    rw [â† mul_inv]
    congr 1
    rw [show ((d : â„‚) * â†‘z + â†‘n) ^ 2 = (-â†‘d * â†‘z - â†‘n) ^ 2 by ring, â† mul_pow]
    simp only [Int.cast_neg, neg_mul]
    field_simp [mul_add, ne_zero z]
    ring
  Â· exact fun i hi =>
      EisensteinSeries.linear_left_summable (ne_zero z) (i : â„¤) (k := 2) (by omega)

lemma G2_S_act (z : â„) : (z.1 ^ 2)â»Â¹ * G2 (ModularGroup.S â€¢ z) = limUnder (atTop)
    fun N : â„• => (âˆ‘' (n : â„¤), âˆ‘ m âˆˆ Ico (-N : â„¤) N, (1 / ((n : â„‚) * z + m) ^ 2)) := by
  rw [modular_S_smul, G2_Ico, limUnder_mul_const _ (aux_cauchySeq_Ico _)]
  congr
  ext N
  simpa [UpperHalfPlane.coe, e2Summand, eisSummand, UpperHalfPlane.mk] using
    (aux_sum_Ico_S_indentity z N)

end transform
