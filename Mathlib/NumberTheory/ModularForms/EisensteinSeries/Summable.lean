/-
Copyright (c) 2024 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/
module

public import Mathlib.Analysis.Complex.UpperHalfPlane.Topology
public import Mathlib.Analysis.PSeries
public import Mathlib.Order.Interval.Finset.Box
public import Mathlib.Analysis.Asymptotics.Defs

/-!
# Summability of Eisenstein series

We gather results about the summability of Eisenstein series, particularly
the summability of the Eisenstein series summands, which are used in the proof of the
boundedness of Eisenstein series at infinity.
-/

@[expose] public section
noncomputable section

open Complex UpperHalfPlane Set Finset Topology Filter Asymptotics

open scoped UpperHalfPlane Topology BigOperators Nat

variable (z : ‚Ñç)

namespace EisensteinSeries

lemma norm_eq_max_natAbs (x : Fin 2 ‚Üí ‚Ñ§) : ‚Äñx‚Äñ = max (x 0).natAbs (x 1).natAbs := by
  rw [‚Üê coe_nnnorm, ‚Üê NNReal.coe_natCast, NNReal.coe_inj, Nat.cast_max]
  refine eq_of_forall_ge_iff fun c ‚Ü¶ ?_
  simp only [pi_nnnorm_le_iff, Fin.forall_fin_two, max_le_iff, NNReal.natCast_natAbs]

lemma norm_symm (x y : ‚Ñ§) : ‚Äñ![x, y]‚Äñ = ‚Äñ![y, x]‚Äñ := by
  simp [EisensteinSeries.norm_eq_max_natAbs, max_comm]

theorem abs_le_left_of_norm (m n : ‚Ñ§) : |n| ‚â§ ‚Äñ![n, m]‚Äñ := by
  simp only [EisensteinSeries.norm_eq_max_natAbs, Fin.isValue, Matrix.cons_val_zero,
    Matrix.cons_val_one, Matrix.cons_val_fin_one, Nat.cast_max, le_sup_iff]
  left
  rw [Int.abs_eq_natAbs]
  exact le_refl _

theorem abs_le_right_of_norm (m n : ‚Ñ§) : |m| ‚â§ ‚Äñ![n, m]‚Äñ := by
  simp only [EisensteinSeries.norm_eq_max_natAbs, Fin.isValue, Matrix.cons_val_zero,
    Matrix.cons_val_one, Matrix.cons_val_fin_one, Nat.cast_max, le_sup_iff]
  right
  rw [Int.abs_eq_natAbs]
  exact le_refl _

lemma abs_norm_eq_max_natAbs (n : ‚Ñï) : ‚Äñ![1, (n + 1 : ‚Ñ§)]‚Äñ = n + 1 := by
  simp only [EisensteinSeries.norm_eq_max_natAbs, Matrix.cons_val_zero, Matrix.cons_val_one,
    Matrix.cons_val_fin_one]
  norm_cast

lemma abs_norm_eq_max_natAbs_neg (n : ‚Ñï) : ‚Äñ![1, -(n + 1 : ‚Ñ§)]‚Äñ = n + 1 := by
  simp only [EisensteinSeries.norm_eq_max_natAbs, Matrix.cons_val_zero, Matrix.cons_val_one,
    Matrix.cons_val_fin_one]
  norm_cast

section bounding_functions

/-- Auxiliary function used for bounding Eisenstein series, defined as
  `z.im ^ 2 / (z.re ^ 2 + z.im ^ 2)`. -/
def r1 : ‚Ñù := z.im ^ 2 / (z.re ^ 2 + z.im ^ 2)

lemma r1_eq : r1 z = 1 / ((z.re / z.im) ^ 2 + 1) := by
  rw [div_pow, div_add_one (by positivity), one_div_div, r1]

lemma r1_pos : 0 < r1 z := by
  dsimp only [r1]
  positivity

/-- For `c, d ‚àà ‚Ñù` with `1 ‚â§ d ^ 2`, we have `r1 z ‚â§ |c * z + d| ^ 2`. -/
lemma r1_aux_bound (c : ‚Ñù) {d : ‚Ñù} (hd : 1 ‚â§ d ^ 2) :
    r1 z ‚â§ (c * z.re + d) ^ 2 + (c * z.im) ^ 2 := by
  have H1 : (c * z.re + d) ^ 2 + (c * z.im) ^ 2 =
    c ^ 2 * (z.re ^ 2 + z.im ^ 2) + d * 2 * c * z.re + d ^ 2 := by ring
  have H2 : (c ^ 2 * (z.re ^ 2 + z.im ^ 2) + d * 2 * c * z.re + d ^ 2) * (z.re ^ 2 + z.im ^ 2)
    - z.im ^ 2 = (c * (z.re ^ 2 + z.im ^ 2) + d * z.re) ^ 2 + (d ^ 2 - 1) * z.im ^ 2 := by ring
  rw [r1, H1, div_le_iff‚ÇÄ (by positivity), ‚Üê sub_nonneg, H2]
  exact add_nonneg (sq_nonneg _) (mul_nonneg (sub_nonneg.mpr hd) (sq_nonneg _))

/-- This function is used to give an upper bound on the summands in Eisenstein series; it is
defined by `z ‚Ü¶ min z.im ‚àö(z.im ^ 2 / (z.re ^ 2 + z.im ^ 2))`. -/
def r : ‚Ñù := min z.im ‚àö(r1 z)

lemma r_pos : 0 < r z := by
  simp only [r, lt_min_iff, im_pos, Real.sqrt_pos, r1_pos, and_self]

lemma r_lower_bound_on_verticalStrip {A B : ‚Ñù} (h : 0 < B) (hz : z ‚àà verticalStrip A B) :
    r ‚ü®‚ü®A, B‚ü©, h‚ü© ‚â§ r z := by
  apply min_le_min hz.2
  rw [Real.sqrt_le_sqrt_iff (by apply (r1_pos z).le)]
  simp only [r1_eq, div_pow, one_div]
  rw [inv_le_inv‚ÇÄ (by positivity) (by positivity), add_le_add_iff_right, ‚Üê even_two.pow_abs]
  gcongr
  exacts [hz.1, hz.2]

lemma auxbound1 {c : ‚Ñù} (d : ‚Ñù) (hc : 1 ‚â§ c ^ 2) : r z ‚â§ ‚Äñc * (z : ‚ÑÇ) + d‚Äñ := by
  rcases z with ‚ü®z, hz‚ü©
  have H1 : z.im ‚â§ ‚àö((c * z.re + d) ^ 2 + (c * z).im ^ 2) := by
    rw [Real.le_sqrt' hz, im_ofReal_mul, mul_pow]
    exact (le_mul_of_one_le_left (sq_nonneg _) hc).trans <| le_add_of_nonneg_left (sq_nonneg _)
  simpa only [r, norm_def, normSq_apply, add_re, re_ofReal_mul, coe_re, ‚Üê pow_two, add_im, mul_im,
    coe_im, ofReal_im, zero_mul, add_zero, min_le_iff] using Or.inl H1

lemma auxbound2 (c : ‚Ñù) {d : ‚Ñù} (hd : 1 ‚â§ d ^ 2) : r z ‚â§ ‚Äñc * (z : ‚ÑÇ) + d‚Äñ := by
  have H1 : ‚àö(r1 z) ‚â§ ‚àö((c * z.re + d) ^ 2 + (c * z.im) ^ 2) :=
    (Real.sqrt_le_sqrt_iff (by positivity)).mpr (r1_aux_bound _ _ hd)
  simpa only [r, norm_def, normSq_apply, add_re, re_ofReal_mul, coe_re, ofReal_re, ‚Üê pow_two,
    add_im, im_ofReal_mul, coe_im, ofReal_im, add_zero, min_le_iff] using Or.inr H1

lemma div_max_sq_ge_one (x : Fin 2 ‚Üí ‚Ñ§) (hx : x ‚â† 0) :
    1 ‚â§ (x 0 / ‚Äñx‚Äñ) ^ 2 ‚à® 1 ‚â§ (x 1 / ‚Äñx‚Äñ) ^ 2 := by
  refine (max_choice (x 0).natAbs (x 1).natAbs).imp (fun H0 ‚Ü¶ ?_) (fun H1 ‚Ü¶ ?_)
  ¬∑ have : x 0 ‚â† 0 := by
      rwa [‚Üê norm_ne_zero_iff, norm_eq_max_natAbs, H0, Nat.cast_ne_zero, Int.natAbs_ne_zero] at hx
    simp only [norm_eq_max_natAbs, H0, Nat.cast_natAbs, Int.cast_abs, div_pow, sq_abs, ne_eq,
      OfNat.ofNat_ne_zero, not_false_eq_true, pow_eq_zero_iff, Int.cast_eq_zero, this, div_self,
      le_refl]
  ¬∑ have : x 1 ‚â† 0 := by
      rwa [‚Üê norm_ne_zero_iff, norm_eq_max_natAbs, H1, Nat.cast_ne_zero, Int.natAbs_ne_zero] at hx
    simp only [norm_eq_max_natAbs, H1, Nat.cast_natAbs, Int.cast_abs, div_pow, sq_abs, ne_eq,
      OfNat.ofNat_ne_zero, not_false_eq_true, pow_eq_zero_iff, Int.cast_eq_zero, this, div_self,
      le_refl]

lemma r_mul_max_le {x : Fin 2 ‚Üí ‚Ñ§} (hx : x ‚â† 0) : r z * ‚Äñx‚Äñ ‚â§ ‚Äñx 0 * (z : ‚ÑÇ) + x 1‚Äñ := by
  have hn0 : ‚Äñx‚Äñ ‚â† 0 := by rwa [norm_ne_zero_iff]
  have h11 : x 0 * (z : ‚ÑÇ) + x 1 = (x 0 / ‚Äñx‚Äñ * z + x 1 / ‚Äñx‚Äñ) * ‚Äñx‚Äñ := by
    rw [div_mul_eq_mul_div, ‚Üê add_div, div_mul_cancel‚ÇÄ _ (mod_cast hn0)]
  rw [norm_eq_max_natAbs, h11, norm_mul, norm_real, norm_norm, norm_eq_max_natAbs]
  gcongr
  ¬∑ rcases div_max_sq_ge_one x hx with H1 | H2
    ¬∑ simpa only [norm_eq_max_natAbs, ofReal_div, ofReal_intCast] using auxbound1 z (x 1 / ‚Äñx‚Äñ) H1
    ¬∑ simpa only [norm_eq_max_natAbs, ofReal_div, ofReal_intCast] using auxbound2 z (x 0 / ‚Äñx‚Äñ) H2

/-- Upper bound for the summand `|c * z + d| ^ (-k)`, as a product of a function of `z` and a
function of `c, d`. -/
lemma summand_bound {k : ‚Ñù} (hk : 0 ‚â§ k) (x : Fin 2 ‚Üí ‚Ñ§) :
    ‚Äñx 0 * (z : ‚ÑÇ) + x 1‚Äñ ^ (-k) ‚â§ (r z) ^ (-k) * ‚Äñx‚Äñ ^ (-k) := by
  by_cases hx : x = 0
  ¬∑ simp only [hx, Pi.zero_apply, Int.cast_zero, zero_mul, add_zero, norm_zero]
    by_cases h : -k = 0
    ¬∑ rw [h, Real.rpow_zero, Real.rpow_zero, one_mul]
    ¬∑ rw [Real.zero_rpow h, mul_zero]
  ¬∑ rw [‚Üê Real.mul_rpow (r_pos _).le (norm_nonneg _)]
    exact Real.rpow_le_rpow_of_nonpos (mul_pos (r_pos _) (norm_pos_iff.mpr hx)) (r_mul_max_le z hx)
      (neg_nonpos.mpr hk)

variable {z} in
lemma summand_bound_of_mem_verticalStrip {k : ‚Ñù} (hk : 0 ‚â§ k) (x : Fin 2 ‚Üí ‚Ñ§)
    {A B : ‚Ñù} (hB : 0 < B) (hz : z ‚àà verticalStrip A B) :
    ‚Äñx 0 * (z : ‚ÑÇ) + x 1‚Äñ ^ (-k) ‚â§ r ‚ü®‚ü®A, B‚ü©, hB‚ü© ^ (-k) * ‚Äñx‚Äñ ^ (-k) := by
  refine (summand_bound z hk x).trans (mul_le_mul_of_nonneg_right ?_ (by positivity))
  exact Real.rpow_le_rpow_of_nonpos (r_pos _) (r_lower_bound_on_verticalStrip z hB hz)
    (neg_nonpos.mpr hk)

lemma linear_isTheta_right_add (c e : ‚Ñ§) (z : ‚ÑÇ) :
    (fun d : ‚Ñ§ ‚Ü¶ c * z + d + e) =Œò[cofinite] fun n ‚Ü¶ (n : ‚Ñù) := by
  apply IsTheta.add_isLittleO <;>
  [refine Asymptotics.IsLittleO.add_isTheta ?_ (Int.cast_complex_isTheta_cast_real); skip] <;>
  simpa [-Int.cofinite_eq] using
    .inr <| tendsto_norm_comp_cofinite_atTop_of_isClosedEmbedding Int.isClosedEmbedding_coe_real

@[deprecated linear_isTheta_right_add (since := "2025-12-27")]
lemma linear_isTheta_right (c : ‚Ñ§) (z : ‚ÑÇ) :
    (fun (d : ‚Ñ§) ‚Ü¶ (c * z + d)) =Œò[cofinite] fun n ‚Ü¶ (n : ‚Ñù) := by
  simpa using linear_isTheta_right_add c 0 z

lemma linear_isTheta_left (d : ‚Ñ§) {z : ‚ÑÇ} (hz : z ‚â† 0) :
    (fun (c : ‚Ñ§) ‚Ü¶ (c * z + d)) =Œò[cofinite] fun n ‚Ü¶ (n : ‚Ñù) := by
  apply IsTheta.add_isLittleO
  ¬∑ simp_rw [mul_comm]
    apply Asymptotics.IsTheta.const_mul_left hz Int.cast_complex_isTheta_cast_real
  ¬∑ simp only [isLittleO_const_left, Int.cast_eq_zero,
      tendsto_norm_comp_cofinite_atTop_of_isClosedEmbedding Int.isClosedEmbedding_coe_real, or_true]

lemma linear_inv_isBigO_right_add (c e : ‚Ñ§) (z : ‚ÑÇ) :
    (fun (d : ‚Ñ§) ‚Ü¶ (c * z + d + e)‚Åª¬π) =O[cofinite] fun n ‚Ü¶ (n : ‚Ñù)‚Åª¬π :=
  (linear_isTheta_right_add c e z).inv.isBigO

lemma linear_inv_isBigO_right (c : ‚Ñ§) (z : ‚ÑÇ) :
    (fun (d : ‚Ñ§) ‚Ü¶ (c * z + d)‚Åª¬π) =O[cofinite] fun n ‚Ü¶ (n : ‚Ñù)‚Åª¬π := by
  grind [add_zero, (linear_isTheta_right_add c 0 z).inv.isBigO]

lemma linear_inv_isBigO_left (d : ‚Ñ§) {z : ‚ÑÇ} (hz : z ‚â† 0) :
    (fun (c : ‚Ñ§) ‚Ü¶ (c * z + d)‚Åª¬π) =O[cofinite] fun n ‚Ü¶ (n : ‚Ñù)‚Åª¬π :=
  (linear_isTheta_left d hz).inv.isBigO

lemma tendsto_zero_inv_linear (z : ‚ÑÇ) (b : ‚Ñ§) :
    Tendsto (fun d : ‚Ñï ‚Ü¶ 1 / ((b : ‚ÑÇ) * z + d)) atTop (ùìù 0) := by
  apply IsBigO.trans_tendsto ?_ tendsto_inv_atTop_nhds_zero_nat (F'' := ‚Ñù)
  have := (isBigO_sup.mp (Int.cofinite_eq ‚ñ∏ linear_inv_isBigO_right b z)).2
  simpa [‚Üê Nat.map_cast_int_atTop, isBigO_map]

lemma tendsto_zero_inv_linear_sub (z : ‚ÑÇ) (b : ‚Ñ§) :
    Tendsto (fun d : ‚Ñï ‚Ü¶ 1 / ((b : ‚ÑÇ) * z - d)) atTop (ùìù 0) := by
  grind [neg_zero, (tendsto_zero_inv_linear z (-b)).neg]

end bounding_functions

/-- The function `‚Ñ§ ^ 2 ‚Üí ‚Ñù` given by `x ‚Ü¶ ‚Äñx‚Äñ ^ (-k)` is summable if `2 < k`. We prove this by
splitting into boxes using `Finset.box`. -/
lemma summable_one_div_norm_rpow {k : ‚Ñù} (hk : 2 < k) :
    Summable fun (x : Fin 2 ‚Üí ‚Ñ§) ‚Ü¶ ‚Äñx‚Äñ ^ (-k) := by
  rw [‚Üê (finTwoArrowEquiv _).symm.summable_iff, summable_partition _ Int.existsUnique_mem_box]
  ¬∑ simp only [finTwoArrowEquiv_symm_apply, Function.comp_def]
    refine ‚ü®fun n ‚Ü¶ (hasSum_fintype (Œ≤ := box (Œ± := ‚Ñ§ √ó ‚Ñ§) n) _).summable, ?_‚ü©
    suffices Summable fun n : ‚Ñï ‚Ü¶ ‚àë' (_ : box (Œ± := ‚Ñ§ √ó ‚Ñ§) n), (n : ‚Ñù) ^ (-k) by
      refine this.congr fun n ‚Ü¶ tsum_congr fun p ‚Ü¶ ?_
      simp only [‚Üê Int.mem_box.mp p.2, Nat.cast_max, norm_eq_max_natAbs, Matrix.cons_val_zero,
        Matrix.cons_val_one]
    simp only [tsum_fintype, univ_eq_attach, sum_const, card_attach, nsmul_eq_mul]
    apply ((Real.summable_nat_rpow.mpr (by linarith : 1 - k < -1)).mul_left
      8).of_norm_bounded_eventually_nat
    filter_upwards [Filter.eventually_gt_atTop 0] with n hn
    rw [Int.card_box hn.ne', Real.norm_of_nonneg (by positivity), sub_eq_add_neg,
      Real.rpow_add (Nat.cast_pos.mpr hn), Real.rpow_one, Nat.cast_mul, Nat.cast_ofNat, mul_assoc]
  ¬∑ exact fun n ‚Ü¶ Real.rpow_nonneg (norm_nonneg _) _

/-- If the inverse of a function `isBigO` to `(|(n : ‚Ñù)| ^ a)‚Åª¬π` for `1 < a`, then the function is
Summable. -/
lemma summable_inv_of_isBigO_rpow_inv {Œ± : Type*} [NormedField Œ±] [CompleteSpace Œ±]
    {f : ‚Ñ§ ‚Üí Œ±} {a : ‚Ñù} (hab : 1 < a)
    (hf : (fun n ‚Ü¶ (f n)‚Åª¬π) =O[cofinite] fun n ‚Ü¶ (|(n : ‚Ñù)| ^ a)‚Åª¬π) :
    Summable fun n ‚Ü¶ (f n)‚Åª¬π :=
  summable_of_isBigO
    ((Real.summable_abs_int_rpow hab).congr fun b ‚Ü¶ Real.rpow_neg (abs_nonneg ‚Üëb) a) hf

/-- For `z : ‚ÑÇ` the function `d : ‚Ñ§ ‚Ü¶ ((c z + d) ^ k)‚Åª¬π` is Summable for `2 ‚â§ k`. -/
lemma linear_right_summable (z : ‚ÑÇ) (c : ‚Ñ§) {k : ‚Ñ§} (hk : 2 ‚â§ k) :
    Summable fun d : ‚Ñ§ ‚Ü¶ ((c * z + d) ^ k)‚Åª¬π := by
  apply summable_inv_of_isBigO_rpow_inv (a := k) (by norm_cast)
  lift k to ‚Ñï using by lia
  grind [(linear_inv_isBigO_right c z).abs_right.pow k,
    zpow_natCast, Int.cast_natCast, Real.rpow_natCast, ‚Üê inv_pow, ‚Üê abs_inv]

/-- For `z : ‚ÑÇ` the function `c : ‚Ñ§ ‚Ü¶ ((c z + d) ^ k)‚Åª¬π` is Summable for `2 ‚â§ k`. -/
lemma linear_left_summable {z : ‚ÑÇ} (hz : z ‚â† 0) (d : ‚Ñ§) {k : ‚Ñ§} (hk : 2 ‚â§ k) :
    Summable fun c : ‚Ñ§ ‚Ü¶ ((c * z + d) ^ k)‚Åª¬π := by
  apply summable_inv_of_isBigO_rpow_inv (a := k) (by norm_cast)
  lift k to ‚Ñï using (by lia)
  simp only [zpow_natCast, Int.cast_natCast, Real.rpow_natCast, ‚Üê inv_pow, ‚Üê abs_inv]
  apply (linear_inv_isBigO_left d hz).abs_right.pow

lemma summable_linear_sub_mul_linear_add (z : ‚ÑÇ) (c‚ÇÅ c‚ÇÇ : ‚Ñ§) :
    Summable fun n : ‚Ñ§ ‚Ü¶ ((c‚ÇÅ * z - n) * (c‚ÇÇ * z + n))‚Åª¬π := by
  apply summable_inv_of_isBigO_rpow_inv (a := 2) (by norm_cast)
  simpa [pow_two] using (linear_inv_isBigO_right c‚ÇÇ z).mul
      (linear_inv_isBigO_right c‚ÇÅ z).comp_neg_int

lemma summable_linear_right_add_one_mul_linear_right (z : ‚ÑÇ) (c‚ÇÅ c‚ÇÇ : ‚Ñ§) :
    Summable fun n : ‚Ñ§ ‚Ü¶ ((c‚ÇÅ * z + n + 1) * (c‚ÇÇ * z + n))‚Åª¬π := by
  apply summable_inv_of_isBigO_rpow_inv (a := 2) (by norm_cast)
  simpa [pow_two] using (linear_inv_isBigO_right c‚ÇÇ z).mul
    (linear_inv_isBigO_right_add c‚ÇÅ 1 z)

lemma summable_linear_left_mul_linear_left {z : ‚ÑÇ} (hz : z ‚â† 0) (c‚ÇÅ c‚ÇÇ : ‚Ñ§) :
    Summable fun n : ‚Ñ§ ‚Ü¶ ((n * z + c‚ÇÅ) * (n * z + c‚ÇÇ))‚Åª¬π := by
  apply summable_inv_of_isBigO_rpow_inv (a := 2) (by norm_cast)
  simp only [Real.rpow_two, abs_mul_abs_self, pow_two]
  simpa using (linear_inv_isBigO_left c‚ÇÇ hz).mul (linear_inv_isBigO_left c‚ÇÅ hz)

private lemma aux_isBigO_linear (z : ‚Ñç) (a b : ‚Ñ§) :
    (fun (m : Fin 2 ‚Üí ‚Ñ§) ‚Ü¶ ((m 0 + a : ‚ÑÇ) * z + m 1 + b)‚Åª¬π) =O[cofinite]
    fun (m : Fin 2 ‚Üí ‚Ñ§) ‚Ü¶ ‚Äñ![m 0 + a, m 1 + b]‚Äñ‚Åª¬π := by
  rw [Asymptotics.isBigO_iff]
  have h0 : z ‚àà verticalStrip |z.re| (z.im) := by simp [mem_verticalStrip_iff]
  use ‚Äñr ‚ü®‚ü®|z.re|, z.im‚ü©, z.2‚ü©‚Äñ‚Åª¬π
  filter_upwards with m
  apply le_trans (by simpa [Real.rpow_neg_one, add_assoc] using
    summand_bound_of_mem_verticalStrip zero_le_one ![m 0 + a, m 1 + b] z.2 h0)
  simp [abs_of_pos (r_pos _)]
  aesop

lemma isLittleO_const_left_of_properSpace_of_discreteTopology
    {Œ± : Type*} (a : Œ±) [NormedAddCommGroup Œ±] [DiscreteTopology Œ±]
    [ProperSpace Œ±] : (fun _ : Œ± ‚Ü¶ a) =o[cofinite] (‚Äñ¬∑‚Äñ) := by
  simpa [isLittleO_const_left, Function.comp_def] using
    .inr <| tendsto_norm_comp_cofinite_atTop_of_isClosedEmbedding IsClosedEmbedding.id

lemma vec_add_const_isTheta (a b : ‚Ñ§) :
    (fun (m : Fin 2 ‚Üí ‚Ñ§) => ‚Äñ![m 0 + a, m 1 + b]‚Äñ‚Åª¬π) =Œò[cofinite] (fun m => ‚Äñm‚Äñ‚Åª¬π) := by
  have (x : Fin 2 ‚Üí ‚Ñ§) : ![x 0 + a, x 1 + b] = x + ![a, b] := List.ofFn_inj.mp rfl
  simpa only [isTheta_inv, isTheta_norm_left, this] using (IsTheta.add_isLittleO
  (by rw [‚Üê isTheta_norm_left]) (isLittleO_const_left_of_properSpace_of_discreteTopology ![a, b]))

lemma isBigO_linear_add_const_vec (z : ‚Ñç) (a b : ‚Ñ§) :
    (fun m : (Fin 2 ‚Üí ‚Ñ§) => (((m 0 : ‚ÑÇ) + a) * z + m 1 + b)‚Åª¬π) =O[cofinite] (fun m => ‚Äñm‚Äñ‚Åª¬π) :=
  (aux_isBigO_linear z a b).trans (vec_add_const_isTheta a b).isBigO

/-- If a function `‚Ñ§¬≤ ‚Üí ‚ÑÇ` is `O (‚Äñn‚Äñ ^ a)‚Åª¬π` for `2 < a`, then the function is summable. -/
lemma summable_of_isBigO_rpow_norm {E : Type*} [NormedAddCommGroup E] [CompleteSpace E]
    {f : (Fin 2 ‚Üí ‚Ñ§) ‚Üí E} {a : ‚Ñù} (hab : 2 < a)
    (hf : f =O[cofinite] fun n ‚Ü¶ (‚Äñn‚Äñ ^ a)‚Åª¬π) : Summable f :=
  summable_of_isBigO
    ((summable_one_div_norm_rpow hab).congr fun b ‚Ü¶ Real.rpow_neg (norm_nonneg b) a) hf

end EisensteinSeries
