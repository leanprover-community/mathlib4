/-
Copyright (c) 2025 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/

import Mathlib.NumberTheory.ModularForms.EisensteinSeries.E2.Summable
import Mathlib.LinearAlgebra.Matrix.FixedDetMatrices

/-!
# Eisenstein Series E2

We define the Eisenstein series `E2` of weight `2` and level `1` as a limit of partial sums
over non-symmetric intervals.

-/

open UpperHalfPlane hiding I

open ModularForm EisensteinSeries  TopologicalSpace  intervalIntegral
  Metric Filter Function Complex MatrixGroups Finset ArithmeticFunction Set SummationFilter

open scoped Interval Real Topology BigOperators Nat

noncomputable section

/-- This is an auxiliary correction term for proving how E2 transforms. It allows us to work with
nicer indexing sets for our infinite sums. -/
private def Î´ (x : Fin 2 â†’ â„¤) : â„‚ := if x = ![0,0] then 1 else if x = ![0, -1] then 2 else 0

@[simp]
private lemma Î´_eq : Î´ ![0,0] = 1 := by simp [Î´]

private lemma Î´_eq2 : Î´ ![0, -1] = 2 := by simp [Î´]

private lemma Î´_neq (a b : â„¤) (h : a â‰  0) : Î´ ![a,b] = 0 := by
  simp [Î´, h]

namespace EisensteinSeries

/-- This gives term gives and alternative infinte sum for G2 which is absolutely convergent. -/
abbrev G2Term (z : â„) (m : Fin 2 â†’ â„¤) : â„‚ := (((m 0 : â„‚) * z + m 1) ^ 2 * (m 0 * z + m 1 + 1))â»Â¹

/-- The map that swaps the two co-ordinates of a `Fin 2 â†’ Î±` -/
def swap {Î± : Type*} : (Fin 2 â†’ Î±) â†’ (Fin 2 â†’ Î±) := fun x â†¦ ![x 1, x 0]

@[simp]
lemma swap_apply {Î± : Type*} (b : Fin 2 â†’ Î±) : swap b = ![b 1, b 0] := rfl

lemma swap_involutive {Î± : Type*} (b : Fin 2 â†’ Î±) : swap (swap b) = b := by
  ext i
  fin_cases i <;> rfl

/-- An equivalence between `Fin 2 â†’ Î±` and itself given by swapping the two co-ordinates -/
def swap_equiv {Î± : Type*} : Equiv (Fin 2 â†’ Î±) (Fin 2 â†’ Î±) := Equiv.mk swap swap
  (by rw [Function.LeftInverse]; apply swap_involutive)
  (by rw [Function.RightInverse]; apply swap_involutive)

section AuxiliaryLemmas

private lemma G2_partial_sum_eq (z : â„) (N : â„•) : âˆ‘ m âˆˆ Icc (-N : â„¤) N, e2Summand m z =
    (2 * riemannZeta 2) + (âˆ‘ m âˆˆ range N, -8 * Ï€ ^ 2  *
    âˆ‘' n : â„•+, n  * cexp (2 * Ï€ * I  * z) ^ ((m + 1) * n : â„•)) := by
  rw [sum_Icc_of_even_eq_range (e2Summand_even z), Finset.sum_range_succ', smul_add]
  nth_rw 2 [two_mul]
  ring_nf
  have H (a : â„•) := EisensteinSeries.qExpansion_identity_pnat (k := 1) (by grind)
    âŸ¨(a + 1) * z, by simpa [show  0 < a + (1 : â„) by positivity] using z.2âŸ©
  simp only [coe_mk_subtype, add_comm, Nat.reduceAdd, one_div, mul_comm, mul_neg, even_two,
    Even.neg_pow, Nat.factorial_one, Nat.cast_one, div_one, pow_one, e2Summand, eisSummand,
    Nat.cast_add, Fin.isValue, Matrix.cons_val_zero, Int.cast_add, Int.cast_natCast, Int.cast_one,
    Matrix.cons_val_one, Matrix.cons_val_fin_one, Int.reduceNeg, zpow_neg, mul_sum, Int.cast_zero,
    zero_mul, add_zero] at *
  congr
  Â· simpa using (two_mul_riemannZeta_eq_tsum_int_inv_pow_of_even (by grind) even_two).symm
  Â· ext a
    norm_cast at *
    simp_rw [H a, â† tsum_mul_left, â† tsum_neg,ofReal_mul, ofReal_ofNat, mul_pow, I_sq, neg_mul,
      one_mul, Nat.cast_add, Nat.cast_one, mul_neg, ofReal_pow, â† exp_nsmul, nsmul_eq_mul,
      Nat.cast_mul]
    exact tsum_congr fun b â†¦ by grind [exp_add]

private lemma aux_G2_tendsto (z : â„) : Tendsto (fun N â†¦ âˆ‘ m âˆˆ range N, -8 * Ï€ ^ 2 *
    âˆ‘' (n : â„•+), n * cexp (2 * Ï€ * I * z) ^ ((m + 1) * n : â„•)) atTop
    (ğ“ (-8 * Ï€ ^ 2 * âˆ‘' (n : â„•+), (Ïƒ 1 n) * cexp (2 * Ï€ * I * z) ^ (n : â„•))) := by
  have : -8 * Ï€ ^ 2 * âˆ‘' (n : â„•+), (Ïƒ 1 n) * cexp (2 * Ï€ * I * z) ^ (n : â„•) =
    âˆ‘' m : â„•, (-8 * Ï€ ^ 2  * âˆ‘' n : â„•+, n * cexp (2 * Ï€ * I * z) ^ ((m + 1) * n)) := by
    have := tsum_prod_pow_eq_tsum_sigma 1 (norm_exp_two_pi_I_lt_one z)
    rw [tsum_pnat_eq_tsum_succ (f := fun d â†¦
      âˆ‘' (c : â„•+), (c ^ 1 : â„‚) * cexp (2 * Ï€ * I * z) ^ (d * c : â„•))] at this
    simp [â† tsum_mul_left, â† this]
  rw [this]
  have hf : Summable fun m : â„• â†¦ (-8 * Ï€ ^ 2 *
      âˆ‘' n : â„•+, n * cexp (2 * Ï€ * I * z) ^ ((m + 1) * n : â„•)) := by
    apply Summable.mul_left
    rw [â† summable_pnat_iff_summable_succ
      (f := fun b â†¦ âˆ‘' (c : â„•+), c * cexp (2 * Ï€ * I * z) ^ (b * c : â„•))]
    apply ((summable_prod_mul_pow 1 (UpperHalfPlane.norm_exp_two_pi_I_lt_one z)).prod).congr
    simp [â† exp_nsmul]
  simpa using (hf.hasSum).comp tendsto_finset_range

--Do we want this not to be private? I made it more general in case we want it elsewhere.
private lemma tendsto_zero_of_cauchySeq_sum_Icc {F : Type*} [NormedRing F] [NormSMulClass â„¤ F]
    {f : â„¤ â†’ F} (hc : CauchySeq fun N : â„• â†¦ âˆ‘ m âˆˆ Icc (-N : â„¤) N, f m) (hs : f.Even) :
    Tendsto f atTop (ğ“ 0) := by
  simp only [cauchySeq_iff_le_tendsto_0, Metric.tendsto_atTop, gt_iff_lt, ge_iff_le,
    dist_zero_right, Real.norm_eq_abs] at *
  obtain âŸ¨g, hg, H, HgâŸ© := hc
  intro Îµ hÎµ
  obtain âŸ¨N, hNâŸ© := (Hg (2 * Îµ) (by positivity))
  refine âŸ¨N + 1, fun n hn â†¦ ?_âŸ©
  have H2 := (H n.natAbs (n -1).natAbs N (by omega) (by omega))
  rw [sum_Icc_add_endpoints f (by omega)] at H2
  have h1 : |n| = n := by
    rw [abs_eq_self]
    omega
  have h2 : |n - 1| = n - 1 := by
    rw [abs_eq_self, Int.sub_nonneg]
    omega
  have := norm_smul (2 : â„¤) (f n)
  simp only [Nat.cast_natAbs, h1, Int.cast_eq, (hs n), (two_mul (f n)).symm, neg_sub, h2,
    Int.cast_sub, Int.cast_one, dist_add_self_left, zsmul_eq_mul, Int.cast_ofNat] at *
  simpa [this, Int.norm_eq_abs] using lt_of_le_of_lt (le_trans H2 (le_abs_self (g N)))
    (hN N (by rfl))

private lemma aux_sum_Ico_S_indentity (z : â„) (N : â„•) :
    ((z : â„‚) ^ 2)â»Â¹ * (âˆ‘ x âˆˆ Ico (-N : â„¤) N, âˆ‘' (n : â„¤), (((x : â„‚) * (-â†‘z)â»Â¹ + n) ^ 2)â»Â¹) =
    âˆ‘' (n : â„¤), âˆ‘ x âˆˆ Ico (-N : â„¤) N, (((n : â„‚) * z + x) ^ 2)â»Â¹ := by
  simp_rw [inv_neg, mul_neg]
  rw [Finset.mul_sum, Summable.tsum_finsetSum
    (by apply fun i hi => linear_left_summable (ne_zero z) (i : â„¤) (k := 2) (by omega))]
  apply sum_congr rfl fun n hn â†¦ ?_
  rw [â† tsum_mul_left, â† tsum_comp_neg]
  apply tsum_congr fun d â†¦ ?_
  rw [â† mul_inv,  â† mul_pow, â† neg_pow_two]
  congr
  field_simp [ne_zero z]
  simp

lemma G2_S_act (z : â„) :
    Tendsto (fun N : â„• â†¦ (âˆ‘' (n : â„¤), âˆ‘ m âˆˆ Ico (-N : â„¤) N, (1 / ((n : â„‚) * z + m) ^ 2))) atTop
      (ğ“ ((z.1 ^ 2)â»Â¹ * G2 (ModularGroup.S â€¢ z))) := by
  rw [G2_eq_tsum_IcoFilter, â† tsum_mul_left]
  have : Summable (fun m : â„¤ â†¦ (z.1 ^ 2)â»Â¹ * e2Summand m (ModularGroup.S â€¢ z)) (IcoFilter â„¤) := by
    apply (Summable_IccFilter_G2_Ico (ModularGroup.S â€¢ z)).mul_left
  have := (this.hasSum)
  simp only [HasSum, IcoFilter, tendsto_map'_iff, modular_S_smul, â† Nat.map_cast_int_atTop] at *
  apply this.congr
  intro N
  simpa [UpperHalfPlane.coe, e2Summand, eisSummand, UpperHalfPlane.mk, â† mul_sum]
    using (aux_sum_Ico_S_indentity z N)

private lemma telescope_aux (z : â„‚) (m : â„¤) (b : â„•) :
    âˆ‘ n âˆˆ Ico (-b : â„¤) b, (1 / ((m : â„‚) * z + n) - 1 / (m * z + n + 1)) =
    1 / (m * z - b) - 1 / (m * z + b) := by
  induction b with
  | zero => aesop
  | succ b ihb =>
    simp only [Nat.cast_add, Nat.cast_one, one_div, Finset.sum_sub_distrib] at *
    rw [Ico_succ_succ, Finset.sum_union (by simp), Finset.sum_union (by simp),
      Finset.sum_pair (by omega), Finset.sum_pair (by omega), add_sub_add_comm]
    simp only [ihb, neg_add_rev, Int.reduceNeg, Int.cast_add, Int.cast_neg, Int.cast_one,
      Int.cast_natCast]
    ring

lemma tendstozero_inv_linear (z : â„‚) (b : â„¤) :
    Tendsto (fun d : â„• â†¦ 1 / ((b : â„‚) * z + d)) atTop (ğ“ 0) := by
  apply Asymptotics.IsBigO.trans_tendsto ?_ tendsto_inverse_atTop_nhds_zero_nat
  have := (Asymptotics.isBigO_sup.mp (Int.cofinite_eq â–¸ linear_inv_isBigO_right b z)).2
  simpa [â† Nat.map_cast_int_atTop, Asymptotics.isBigO_map] using this

lemma tendstozero_inv_linear_sub (z : â„) (b : â„¤) :
    Tendsto (fun d : â„• â†¦ 1 / ((b : â„‚) * z - d)) atTop (ğ“ 0) := by
  have := (tendstozero_inv_linear z (-b)).neg
  simp only [Int.cast_neg, neg_mul, one_div, neg_zero, â† inv_neg] at *
  exact this.congr (fun _ â†¦ by ring)

private lemma G2_S_action' (z : â„) : âˆ‘'[IcoFilter â„¤] n : â„¤, (âˆ‘' m : â„¤, 1 / ((m : â„‚) * z + n) ^ 2) =
    ((z.1 ^ 2)â»Â¹ * G2 (ModularGroup.S â€¢ z)) := by
  apply HasSum.tsum_eq
  rw [HasSum_IcoFilter_iff]
  apply ( G2_S_act z).congr
  intro x
  rw [Summable.tsum_finsetSum]
  exact fun i hi => by simpa using linear_left_summable (ne_zero z) (i : â„¤) (k := 2) (by omega)

lemma tsumFilter_Ico_eq_zero (z : â„) (m : â„¤) :
    âˆ‘'[IcoFilter â„¤] n : â„¤, (1 / ((m : â„‚) * z + n) - 1 / (m * z + n + 1)) = 0 := by
  apply HasSum.tsum_eq
  simp only [HasSum_IcoFilter_iff] at *
  conv =>
    enter [1, N]
    rw [telescope_aux z m N]
  simpa using Filter.Tendsto.sub (tendstozero_inv_linear_sub z m) (tendstozero_inv_linear z m)

private lemma linear_sub_linear_eq (z : â„) (a b m : â„¤) (hm : m â‰  0) :
    1 / ((m : â„‚) * z + a) - 1 / (m * z + b) = (b - a) * (1 / ((m * z + a) * (m * z + b))) := by
  rw [â† one_div_mul_sub_mul_one_div_eq_one_div_add_one_div]
  Â· simp only [one_div, add_sub_add_left_eq_sub, mul_inv_rev]
    ring
  Â· simpa using UpperHalfPlane.linear_ne_zero z (cd := ![m, a]) (by simp [hm])
  Â· simpa using UpperHalfPlane.linear_ne_zero z (cd := ![m, b]) (by simp [hm])

private lemma linear_sub_linear_eq' (z : â„) (b m : â„¤) (hm1 : b â‰  0) (hm2 : b + 1 â‰  0) :
    1 / ((m : â„‚) * z + b) - 1 / (m * z + b + 1) = 1 / ((m * z + b + 1) * (m * z + b)) := by
  rw [â† one_div_mul_sub_mul_one_div_eq_one_div_add_one_div]
  Â· simp only [one_div, mul_inv_rev]
    ring
  Â· simpa using UpperHalfPlane.linear_ne_zero z (cd := ![m, b]) (by simp [hm1])
  Â· simpa [add_assoc] using UpperHalfPlane.linear_ne_zero z (cd := ![m, b + 1])
      (by simp; intro h; norm_cast at *)

lemma summable_one_div_linear_sub_one_div_linear (z : â„) (a b : â„¤) :
    Summable fun m : â„¤ â†¦ 1 / (m * (z : â„‚) + a) - 1 / (m * z + b) := by
  have := Summable.mul_left  (b - a : â„‚) (summable_linear_mul_linear (ne_zero z) a b)
  rw [â† Finset.summable_compl_iff (s := {0})] at *
  apply this.congr
  intro m
  rw [linear_sub_linear_eq z a b m (by grind)]
  simp

private lemma summable_one_div_linear_sub_one_div_linear_succ (z : â„) (a : â„¤) :
    Summable fun b : â„¤ â†¦ 1 / ((a : â„‚) * â†‘z + â†‘b) - 1 / ((a : â„‚) * â†‘z + â†‘b + 1) := by
  have := (summable_linear_add_mul_linear_add z a a)
  rw [â† Finset.summable_compl_iff (s := {0, -1})] at *
  apply this.congr
  intro m
  have := linear_sub_linear_eq' z m a (by grind) (by grind)
  simp [add_assoc] at *
  rw [â† this]

private lemma aux_tsum_identity_1 (z : â„) (d : â„•+) :
    âˆ‘' (m : â„¤), (1 / ((m : â„‚) * z - d) - 1 / (m * z + d)) = -(2 / d) +
    âˆ‘' m : â„•+, (1 / ((m : â„‚) * z - d) + 1 / (-m * z + -d) - 1 / ((m : â„‚) * z + d) -
    1 / (-m * z + d)) := by
  rw [eq_neg_add_iff_add_eq (b := 2 / (d : â„‚)), tsum_int_eq_zero_add_tsum_pnat]
  Â· simp only [Int.cast_zero, zero_mul, zero_sub, one_div, zero_add, Int.cast_natCast, Int.cast_neg,
      neg_mul]
    ring_nf
    rw [â†  Summable.tsum_add]
    Â· grind
    Â· apply (summable_pnat_iff_summable_nat.mpr ((summable_int_iff_summable_nat_and_neg.mp
        (summable_one_div_linear_sub_one_div_linear z (-d) d)).1)).congr
      grind [Int.cast_natCast, Int.cast_neg, one_div]
    Â· apply (summable_pnat_iff_summable_nat.mpr ((summable_int_iff_summable_nat_and_neg.mp
        (summable_one_div_linear_sub_one_div_linear z (-d) d)).2)).congr
      grind [Int.cast_neg, Int.cast_natCast, neg_mul, one_div]
  Â· apply (summable_one_div_linear_sub_one_div_linear z (-d) d).congr
    grind [Int.cast_neg, Int.cast_natCast, one_div, sub_left_inj, inv_inj]

private lemma aux_tsum_identity_2 (z : â„) (d : â„•+) :
    âˆ‘' m : â„•+, ((1 / ((m : â„‚) * z - d) + 1 / (-m * z + -d)) - (1 / (m * z + d)) -
    1 / (-m * z + d)) = (2 / z) * âˆ‘' m : â„•+, ((1 / (-(d : â„‚) / z - m) + 1 / (-d / z + m))) := by
  rw [â† Summable.tsum_mul_left]
  Â· apply tsum_congr (fun m â†¦ ?_)
    simp_rw [sub_eq_add_neg, â† div_neg, add_comm]
    ring_nf
    field_simp [ne_zero z]
  Â· have := (Summable_cotTerm (x := -d / (z : â„‚))
      (by simpa using int_div_upperHalfPlane_mem_integerComplement z (-d) (by simp at *)))
    simp only [cotTerm, one_div] at *
    conv at this =>
      enter [1, n]
      rw [show ((n : â„‚) + 1) = (n + 1 : â„•) by simp]
    rw [summable_nat_add_iff (k := 1)
      (f := fun n â†¦ (-d / (z : â„‚) - n)â»Â¹ + (-d / (z : â„‚) + n)â»Â¹)] at this
    apply this.subtype

lemma pnat_div_upperHalfPlane_im_pos (n : â„•+) (z : â„) : 0 < (-(n : â„‚) / z).im := by
  simp only [div_im, neg_im, natCast_im, neg_zero, coe_re, zero_mul, zero_div, neg_re, natCast_re,
    coe_im, neg_mul, zero_sub, Left.neg_pos_iff, div_neg_iff, Left.neg_neg_iff, Nat.cast_pos,
    PNat.pos, mul_pos_iff_of_pos_left, Complex.normSq_pos, ne_eq]
  right
  simpa using âŸ¨z.2, ne_zero zâŸ©

private lemma aux_tendsto_tsum_cexp_pnat (z : â„) :
    Tendsto (fun N : â„•+ â†¦ âˆ‘' (n : â„•+), cexp (2 * Ï€ * I * (-N / z)) ^ (n : â„•)) atTop (ğ“ 0) := by
  have := tendsto_zero_geometric_tsum_pnat (UpperHalfPlane.norm_exp_two_pi_I_lt_one âŸ¨-1 / z,
    by simpa using (pnat_div_upperHalfPlane_im_pos 1 z)âŸ©)
  simp only [â† exp_nsmul, mul_neg, neg_div] at *
  apply this.congr fun n â†¦ ?_
  congr
  grind [one_div, coe_mk_subtype, mul_neg, smul_neg, nsmul_eq_mul, Nat.cast_mul, neg_inj]

private theorem aux_tendsto_tsum (z : â„) : Tendsto (fun n : â„• â†¦ (2 / z *
    âˆ‘' (m : â„•+), (1 / (-(n : â„‚) / z - m) + 1 / (-n / z + m)))) atTop (ğ“ (-2 * Ï€ * I / z)) := by
  suffices Tendsto (fun n : â„•+ â†¦ (2 / (z : â„‚) * âˆ‘' (m : â„•+),
      (1 / (-(n : â„‚) / z - m) + 1 / (-n / z + m)))) atTop (ğ“ (-2 * Ï€ * I / z)) by
    rw [â† tendsto_comp_val_Ioi_atTop]
    exact this
  have H0 : (fun n : â„•+ â†¦ (2 / z * âˆ‘' (m : â„•+), (1 / (-(n : â„‚) / z - m) + 1 / (-n / z + m)))) =
      (fun N : â„•+ â†¦ (-2 * Ï€ * I / z) - (2 / z * (2 * Ï€ * I)) *
      (âˆ‘' n : â„•+, cexp (2 * Ï€ * I * (-N / z)) ^ (n : â„•)) + 2 / N) := by
    ext N
    let Z : â„ := âŸ¨-N / z, pnat_div_upperHalfPlane_im_pos N zâŸ©
    have h2 := cot_series_rep (UpperHalfPlane.coe_mem_integerComplement Z)
    rw [pi_mul_cot_pi_q_exp, â† sub_eq_iff_eq_add',coe_mk_subtype, one_div, inv_div, neg_mul] at *
    rw [â† h2, â† tsum_zero_pnat_eq_tsum_nat
    (by simpa using norm_exp_two_pi_I_lt_one âŸ¨-N / z, pnat_div_upperHalfPlane_im_pos N zâŸ©), mul_sub]
    field_simp [ne_zero z]
    ring
  rw [H0]
  nth_rw 2 [show -2 * Ï€ * I / z = (-2 * Ï€ * I / z) - (2 / z * (2 * Ï€ * I)) * 0 + 2 * 0 by ring]
  apply Tendsto.add (Tendsto.sub (by simp) ((aux_tendsto_tsum_cexp_pnat z).const_mul _))
  apply Tendsto.const_mul
  have H4 : Tendsto (fun x : â„• â†¦ 1 / (x : â„‚)) atTop (ğ“ 0) := by
      simpa using tendstozero_inv_linear z 0
  simpa using tendsto_comp_val_Ioi_atTop.mpr H4

private lemma tendsto_tsum_one_div_linear_sub_succ_eq (z : â„) :
    Tendsto (fun N : â„•+ â†¦ âˆ‘ n âˆˆ Ico (-N : â„¤) N,
    âˆ‘' m : â„¤, (1 / ((m : â„‚) * z + n) - 1 / (m * z + n + 1))) atTop (ğ“ (-2 * Ï€ * I / z)) := by
  have : (fun N : â„•+ â†¦ âˆ‘ n âˆˆ (Ico (-N : â„¤) N),
      âˆ‘' m : â„¤ , (1 / ((m : â„‚) * z + n) -  1 / (m * z + n + 1))) = (fun N : â„•+ â†¦
      âˆ‘' m : â„¤ ,  âˆ‘ n âˆˆ Ico (-N : â„¤) N, (1 / ((m : â„‚) * z + n) -  1 / (m * z + n + 1))) := by
    ext n
    rw [Summable.tsum_finsetSum]
    intro i hi
    apply (summable_one_div_linear_sub_one_div_linear z ((i : â„¤)) (i + 1 : â„¤)).congr
    grind [one_div, Int.cast_add, Int.cast_one, sub_right_inj, inv_inj]
  conv at this =>
    enter [2, n]
    conv =>
      enter [1, m]
      rw [telescope_aux z]
    rw [show (n : â„‚) = (n : â„•+) by simp, aux_tsum_identity_1 z]
  rw [this, show -2 * Ï€ * I / z = 0 + -2 * Ï€ * I / z by ring]
  apply Tendsto.add
  Â· have : Tendsto (fun x : â„• â†¦ -(2 / (x : â„‚))) atTop (ğ“ 0) := by
      simpa [tendsto_zero_iff_norm_tendsto_zero] using Filter.Tendsto.const_div_atTop
        (g := fun n : â„• â†¦ â€–(n : â„‚)â€–) (r := 2) (by simpa using tendsto_natCast_atTop_atTop)
    exact tendsto_comp_val_Ioi_atTop.mpr this
  Â· conv =>
      enter [1, n]
      rw [aux_tsum_identity_2]
    have HH := aux_tendsto_tsum z
    rw [â† tendsto_comp_val_Ioi_atTop] at HH
    exact HH

--these are the two key lemmas

lemma tsumFilter_tsum_eq (z : â„) :
    âˆ‘'[IcoFilter â„¤] n : â„¤, âˆ‘' m : â„¤, (1 / ((m : â„‚) * z + n) - 1 / (m * z + n + 1)) =
    -2 * Ï€ * I / z := by
  apply HasSum.tsum_eq
  have := (tendsto_tsum_one_div_linear_sub_succ_eq z)
  simp only [one_div, neg_mul, HasSum, IcoFilter, â† Nat.map_cast_int_atTop, Filter.map_map,
    tendsto_map'_iff] at *
  suffices H :
    Tendsto (fun N : â„• â†¦ âˆ‘ n âˆˆ Ico (-N : â„¤) N,
     âˆ‘' m : â„¤, (1 / ((m : â„‚) * z + n) - 1 / (m * z + n + 1))) atTop (ğ“ (-2 * Ï€ * I / z)) by
    simpa using H.congr (by simp)
  exact tendsto_comp_val_Ioi_atTop.mp (tendsto_tsum_one_div_linear_sub_succ_eq z)

lemma tsum_tsumFilter_eq (z : â„) :
    âˆ‘' m : â„¤, (âˆ‘'[IcoFilter â„¤] n : â„¤, (1 / ((m : â„‚) * z + n) - 1 / (m * z + n + 1))) = 0 := by
  convert tsum_zero
  apply tsumFilter_Ico_eq_zero

end AuxiliaryLemmas

section transform

lemma G2Term_summable (z : â„) : Summable (fun m â†¦ (G2Term z m)) := by
  have hk' : 2 < (3 : â„) := by linarith
  apply summable_inv_of_isBigO_rpow_norm_inv hk'
  simpa [pow_three, pow_two, â† mul_assoc] using ((isBigO_linear_add_const_vec z 0 1).mul
    (isBigO_linear_add_const_vec z 0 0)).mul (isBigO_linear_add_const_vec z 0 0)

lemma G2Term_summable_Î´ (z : â„) : Summable fun (m : Fin 2 â†’ â„¤) â†¦ (G2Term z m + Î´ m):= by
  let s : Finset (Fin 2 â†’ â„¤) := {![0,0], ![0,-1]}
  rw [â† Finset.summable_compl_iff s]
  apply ((G2Term_summable z).subtype sá¶œ).congr
  intro b
  have hb1 : b.1 â‰  ![0, 0] := by aesop
  have hb2 : b.1 â‰  ![0, -1] := by aesop
  simp [Î´, hb1, hb2]

lemma G2_prod_summable1_Î´ (z : â„) (b : â„¤) : Summable fun c â†¦ G2Term z ![b,c] + Î´ ![b, c] := by
  have := G2Term_summable_Î´ z
  simp only [G2Term, Fin.isValue, mul_inv_rev, â† (finTwoArrowEquiv _).symm.summable_iff,
    finTwoArrowEquiv_symm_apply, Matrix.cons_val_zero, Matrix.cons_val_one,
    Matrix.cons_val_fin_one] at *
  exact this.prod_factor b

theorem extracted_2_Î´ (z : â„) (b : â„¤) : CauchySeq fun N : â„• â†¦ âˆ‘ n âˆˆ Ico (-N : â„¤) N,
    (G2Term z ![b,n] + Î´ ![b, n]) := by
  apply Filter.Tendsto.cauchySeq (x := âˆ‘' (x : â„¤),
        (((b  : â„‚) * z + x + 1)â»Â¹ * (((b : â„‚) * z + x) ^ 2)â»Â¹  + Î´ ![b, x]))
  simpa [â† Nat.map_cast_int_atTop] using (G2_prod_summable1_Î´ z b).hasSum.comp
    tendsto_Ico_atTop_atTop

theorem extracted_3 (z : â„) (b : â„¤) : CauchySeq fun N : â„• â†¦
  âˆ‘ n âˆˆ Finset.Ico (-N : â„¤) N, (1 / ((b : â„‚) * z + n) - 1 / (b * z + n + 1)) := by
  simp_rw [telescope_aux ]
  apply Filter.Tendsto.cauchySeq
  simpa using Filter.Tendsto.sub (tendstozero_inv_linear_sub z b) (tendstozero_inv_linear z b)

lemma extracted_4 (z : â„) (b : â„¤) :
    CauchySeq fun N : â„• â†¦ âˆ‘ n âˆˆ Ico (-N : â„¤) N, (1 / ((b : â„‚) * z + n) ^ 2 ) := by
  apply Filter.Tendsto.cauchySeq (x := âˆ‘' (x : â„¤), ((((b : â„‚) * z + x) ^ 2)â»Â¹))
  simpa [â† Nat.map_cast_int_atTop] using
    ((linear_right_summable z b  (k := 2) (by norm_num)).hasSum).comp tendsto_Ico_atTop_atTop

lemma poly_id (z : â„) (b n : â„¤) : ((b : â„‚) * z + n + 1)â»Â¹ * (((b : â„‚) * z + n) ^ 2)â»Â¹ +
    (Î´ ![b, n]) + (((b : â„‚) * z + n)â»Â¹ - ((b : â„‚) * z + n + 1)â»Â¹) = (((b : â„‚) * z + n) ^ 2)â»Â¹ := by
  by_cases h : b = 0 âˆ§ n = 0
  Â· simp_rw [h.1, h.2]
    simp
  Â· simp only [not_and] at h
    by_cases hb : b = 0
    Â· by_cases hn : n = -1
      Â· simp only [hb, Int.cast_zero, zero_mul, hn, Int.reduceNeg, Int.cast_neg, Int.cast_one,
        zero_add, neg_add_cancel, inv_zero, even_two, Even.neg_pow, one_pow, inv_one, mul_one,
        Î´_eq2, inv_neg, sub_zero]
        ring
      Â· have hn0 : (n : â„‚) â‰  0 := by aesop
        have hn1 : (n : â„‚) + 1 â‰  0 := by norm_cast; omega
        simp only [hb, Int.cast_zero, zero_mul, zero_add, Î´, Nat.succ_eq_add_one, Nat.reduceAdd,
          Matrix.vecCons_inj, h hb, and_true, and_false, â†“reduceIte, Int.reduceNeg, hn, add_zero]
        field_simp
        ring
    Â· simp only [Î´, Nat.succ_eq_add_one, Nat.reduceAdd, Matrix.vecCons_inj, hb, and_true,
        false_and, â†“reduceIte, Int.reduceNeg, add_zero]
      have h0 : ((b : â„‚) * z + n + 1) â‰  0 := by
        simpa [add_assoc] using linear_ne_zero (cd := ![b, n + 1]) z (by aesop)
      have h1 : ((b : â„‚) * z + n) â‰  0 := by
        simpa using linear_ne_zero (cd := ![b, n]) z (by aesop)
      field_simp
      ring

--this sum is now abs convergent. Idea is to subtract limUnder_sum_eq_zero from the G2 defn.
lemma G2_alt_eq (z : â„) : G2 z = âˆ‘' m, âˆ‘' n, (G2Term z ![m, n] + Î´ ![m, n]) := by
  set t :=  âˆ‘' m, âˆ‘' n,  (G2Term z ![m, n] + Î´ ![m, n])
  rw [G2, show t = t + 0 by ring, â†   tsum_tsumFilter_eq z, â† Summable.tsum_add]
  Â· rw [â† tsum_eq_of_summable_unconditional (L := symCondInt)]
    Â· congr
      ext a
      rw [e2Summand, tsum_eq_of_summable_unconditional (L := IcoFilter â„¤), â† Summable.tsum_add]
      Â· congr
        ext b
        simp [eisSummand, G2Term, poly_id z a b]
        rfl
      Â· apply G2_prod_summable1_Î´
      Â· apply summable_one_div_linear_sub_one_div_linear_succ z a
      Â· apply summable_one_div_linear_sub_one_div_linear_succ z a
    Â· conv =>
        enter [1, N]
        rw [tsumFilter_Ico_eq_zero z N, add_zero]
      apply ((finTwoArrowEquiv _).symm.summable_iff.mpr (G2Term_summable_Î´ z)).prod
  Â· apply (((finTwoArrowEquiv _).symm.summable_iff.mpr (G2Term_summable_Î´ z)).prod).congr
    simp
  Â· apply summable_zero.congr
    intro b
    simp [â† tsumFilter_Ico_eq_zero z b]

lemma G2_inde_lhs (z : â„) : (z.1 ^ 2)â»Â¹ * G2 (ModularGroup.S â€¢ z) - -2 * Ï€ * I / z =
    âˆ‘' n : â„¤, âˆ‘' m : â„¤, (G2Term z ![m, n] + Î´ ![m, n]) := by
  rw [â† tsumFilter_tsum_eq z, â† (G2_S_action' z),
    â† tsum_eq_of_summable_unconditional (L := IcoFilter â„¤), â† Summable.tsum_sub]
  Â· congr
    ext N
    rw [â† Summable.tsum_sub]
    Â· congr
      ext M
      simp only [one_div, G2Term, Fin.isValue, Matrix.cons_val_zero, Matrix.cons_val_one,
        Matrix.cons_val_fin_one, mul_inv_rev]
      have := poly_id z M N
      nth_rw 1 [â† this]
      ring
    Â· simpa using linear_left_summable (ne_zero z) N (k := 2) (by norm_num)
    Â· simpa [add_assoc] using summable_one_div_linear_sub_one_div_linear z N (N + 1)
  Â· apply HasSum.summable (a := (z.1 ^ 2)â»Â¹ * G2 (ModularGroup.S â€¢ z))
    rw [HasSum_IcoFilter_iff]
    have H := G2_S_act z
    apply H.congr
    intro x
    rw [Summable.tsum_finsetSum]
    intro i hi
    simpa using linear_left_summable (ne_zero z) i (k := 2) (by norm_num)
  Â· apply HasSum.summable (a := -2 * Ï€ * I / z)
    rw [HasSum_IcoFilter_iff]
    have H := tendsto_tsum_one_div_linear_sub_succ_eq z
    rw [â† tendsto_comp_val_Ioi_atTop]
    apply H
  Â· have := G2Term_summable_Î´ z
    rw [â† swap_equiv.summable_iff, â† (finTwoArrowEquiv _).symm.summable_iff] at this
    simpa using Summable.prod this

lemma G2_alt_indexing_Î´ (z : â„) : âˆ‘' (m : Fin 2 â†’ â„¤), (G2Term z m + Î´ m)  =
    âˆ‘' m : â„¤, âˆ‘' n : â„¤, (G2Term z ![m, n] + (Î´ ![m, n])) := by
  rw [ â† (finTwoArrowEquiv _).symm.tsum_eq]
  simp only [Fin.isValue, finTwoArrowEquiv_symm_apply, Matrix.cons_val_zero, Matrix.cons_val_one,
    Matrix.cons_val_fin_one, mul_inv_rev]
  refine Summable.tsum_prod' ?_ ?_
  Â· have := G2Term_summable_Î´ z
    simp at this
    rw [â† (finTwoArrowEquiv _).symm.summable_iff] at this
    exact this
  Â· intro b
    have := G2Term_summable_Î´ z
    simp only [Fin.isValue, mul_inv_rev, â† (finTwoArrowEquiv _).symm.summable_iff] at this
    exact this.prod_factor b

lemma G2_alt_indexing2_Î´ (z : â„) : âˆ‘' (m : Fin 2 â†’ â„¤), (G2Term z m + Î´ m)  =
    âˆ‘' n : â„¤, âˆ‘' m : â„¤, (G2Term z ![m, n] + Î´ ![m, n]) := by
  have := (G2Term_summable_Î´ z)
  simp at this
  rw [â† (finTwoArrowEquiv _).symm.summable_iff] at this
  rw [ Summable.tsum_comm', G2_alt_indexing_Î´]
  Â· apply this.congr
    grind [Fin.isValue, finTwoArrowEquiv_symm_apply, Matrix.cons_val_zero,
      Matrix.cons_val_one, Matrix.cons_val_fin_one, mul_inv_rev]
  Â· intro b
    simp only [mul_inv_rev]
    exact this.prod_factor b
  Â· intro c
    have H := (G2Term_summable_Î´ z)
    rw [â† swap_equiv.summable_iff, â† (finTwoArrowEquiv _).symm.summable_iff] at H
    simpa using H.prod_factor c

private lemma G2_S_transform_aux (z : â„) :
    (z.1 ^ 2)â»Â¹ * G2 (ModularGroup.S â€¢ z) - -2 * Ï€ * I / z = G2 z := by
  rw [G2_inde_lhs, G2_alt_eq z , â† G2_alt_indexing2_Î´ , G2_alt_indexing_Î´]

lemma G2_periodic : (G2 âˆ£[(2 : â„¤)] ModularGroup.T) = G2 := by
  ext z
  simp_rw [SL_slash_def, UpperHalfPlane.modular_T_smul z]
  simp only [G2_q_exp, UpperHalfPlane.coe_vadd, ofReal_one, ModularGroup.T,
    ModularGroup.denom_apply, Fin.isValue, Matrix.of_apply, Matrix.cons_val', Matrix.cons_val_zero,
    Matrix.empty_val', Matrix.cons_val_fin_one, Matrix.cons_val_one, Int.cast_zero, zero_mul,
    Int.cast_one, zero_add, Int.reduceNeg, zpow_neg, one_zpow, inv_one, mul_one,
    â† exp_periodic.nat_mul 1 (2 * Ï€ * I * z), Nat.cast_one, one_mul, sub_right_inj,
    mul_eq_mul_left_iff, mul_eq_zero, OfNat.ofNat_ne_zero, ne_eq, not_false_eq_true,
    pow_eq_zero_iff, ofReal_eq_zero, Real.pi_ne_zero, or_self, or_false]
  grind

lemma modular_slash_S_apply (f : â„ â†’ â„‚) (k : â„¤) (z : â„) :
    (f âˆ£[k] ModularGroup.S) z = f (UpperHalfPlane.mk (-z)â»Â¹ z.im_inv_neg_coe_pos) * z ^ (-k) := by
  rw [SL_slash_apply, denom, UpperHalfPlane.modular_S_smul]
  simp [ModularGroup.S]

lemma G2_transform (Î³ : SL(2, â„¤)) : (G2 âˆ£[(2 : â„¤)] Î³) = G2 - (D2 Î³) := by
  have := Subgroup.closure_induction (G := SL(2, â„¤)) (p := fun Î³ _ â†¦ G2 âˆ£[(2 : â„¤)] Î³ = G2 - (D2 Î³))
    (k := ({ModularGroup.S, ModularGroup.T})) ?_ ?_
  Â· apply this
    Â· intro a b ha hb HA HB
      rw [D2_mul, SlashAction.slash_mul, HA, sub_eq_add_neg, SlashAction.add_slash, HB]
      ext z
      simp only [SlashAction.neg_slash, SL_slash, Pi.add_apply, Pi.sub_apply, Pi.neg_apply]
      ring
    Â· intro g hg hg2
      have H1 : (G2 âˆ£[(2 : â„¤)] g)  âˆ£[(2 : â„¤)] gâ»Â¹ = (G2 - D2 g)âˆ£[(2 : â„¤)] gâ»Â¹ := by
        rw [hg2]
      rw [â†  SlashAction.slash_mul, sub_eq_add_neg, SlashAction.add_slash] at H1
      simp only [mul_inv_cancel, SlashAction.slash_one, SL_slash, SlashAction.neg_slash] at H1
      nth_rw 2 [H1]
      rw [â† sub_eq_add_neg]
      have := D2_inv g
      simp only [SL_slash] at this
      rw [this]
      simp only [SL_slash, sub_neg_eq_add, add_sub_cancel_right]
    Â· rw [SpecialLinearGroup.SL2Z_generators]
      simp only [Subgroup.mem_top]
  Â· intro a ha
    simp only [mem_insert_iff, mem_singleton_iff, SL_slash] at *
    rcases ha with h1|h2
    Â· ext z
      simp only [Pi.sub_apply]
      rw [h1, D2_S z]
      have:= modular_slash_S_apply G2 2 z
      simp only [SL_slash, Int.reduceNeg, zpow_neg] at this
      rw [this, mul_comm]
      have := G2_S_transform_aux z
      rw [â† this]
      ring_nf
      rw [modular_S_smul]
      congr
      Â· simp only [UpperHalfPlane.coe, inv_pow, inv_inj]
        norm_cast
      Â· simp only [UpperHalfPlane.coe]
        ring
    Â· simpa only [h2, D2_T, sub_zero] using G2_periodic
  Â· simp only [SlashAction.slash_one, D2_one, sub_zero]

end transform

end EisensteinSeries
