/-
Copyright (c) 2025 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck, David Loeffler
-/

module

public import Mathlib.NumberTheory.ModularForms.EisensteinSeries.E2.Defs
public import Mathlib.NumberTheory.ModularForms.EisensteinSeries.QExpansion

/-!
# Summability of E2

We collect here lemmas about the summability of the Eisenstein series `E2` that will be used to
prove how it transforms under the slash action.

## Main Results

The key results concern the difference between two different orders of summation for the
telescoping series `âˆ‘_{m,n} (1/(mz + n) - 1/(mz + n + 1))`:

1. **`tsum_symmetricIco_tsum_sub_eq`**: Summing first over `n` (in symmetric intervals), then `m`:
   `âˆ‘'[symmetricIco] n : â„¤, âˆ‘' m : â„¤, (1/(mz+n) - 1/(mz+n+1)) = -2Ï€i/z`

2. **`tsum_tsum_symmetricIco_sub_eq`**: Summing first over `m`, then `n` (in symmetric intervals):
   `âˆ‘' m : â„¤, âˆ‘'[symmetricIco] n : â„¤, (1/(mz+n) - 1/(mz+n+1)) = 0`

The difference `-2Ï€i/z` between these two orderings is precisely the correction term
`D2` that appears in the transformation formula for `G2` under the action of `S`.

## Proof Strategy

1. For fixed `m â‰  0`, the inner sum over `n` telescopes to zero (each term cancels with its
   neighbor), establishing the first identity.

2. For fixed `n`, the inner sum over `m` can be computed using the cotangent series expansion.
   As `n â†’ Â±âˆ` in symmetric intervals, these sums contribute `-2Ï€i/z`.

-/

open UpperHalfPlane hiding I Ïƒ

open Filter Complex Finset SummationFilter

open scoped Interval Real Topology BigOperators Nat ArithmeticFunction.sigma

@[expose] public noncomputable section

namespace EisensteinSeries

variable (z : â„)

local notation "ğ•¢" z:100 => cexp (2 * Ï€ * I * z)

private lemma G2_partial_sum_eq (N : â„•) : âˆ‘ m âˆˆ Icc (-N : â„¤) N, e2Summand m z =
    2 * riemannZeta 2 + âˆ‘ m âˆˆ range N, -8 * Ï€ ^ 2 *
      âˆ‘' n : â„•+, n * ğ•¢ z ^ ((m + 1) * n) := by
  rw [sum_Icc_of_even_eq_range (e2Summand_even z), Finset.sum_range_succ', smul_add,
    nsmul_eq_mul, Nat.cast_zero, e2Summand_zero_eq_two_riemannZeta_two]
  ring_nf
  simp only [e2Summand, eisSummand, add_comm, Nat.cast_add, Nat.cast_one, Fin.isValue,
    Matrix.cons_val_zero, Int.cast_add, Int.cast_natCast, Int.cast_one, Matrix.cons_val_one,
    Matrix.cons_val_fin_one, Int.reduceNeg, zpow_neg, mul_comm, mul_sum]
  congr with a
  have H2 := qExpansion_identity_pnat (k := 1) (by grind)
    âŸ¨(a + 1) * z, by simpa [show 0 < ((a + 1) : â„) by positivity] using z.2âŸ©
  simp only [coe_mk_subtype, add_comm, Nat.reduceAdd, one_div, mul_comm, mul_neg, even_two,
    Even.neg_pow, Nat.factorial_one, Nat.cast_one, div_one, pow_one] at H2
  simp_rw [zpow_ofNat, H2, â† tsum_mul_left, â† tsum_neg, â† exp_nsmul]
  refine tsum_congr fun b â†¦ ?_
  ring_nf
  grind [I_sq, exp_add]

private lemma aux_G2_tendsto : Tendsto
    (fun N â†¦ âˆ‘ m âˆˆ range N, -8 * Ï€ ^ 2 * âˆ‘' n : â„•+, n * ğ•¢ z ^ ((m + 1) * n)) atTop
    (ğ“ (-8 * Ï€ ^ 2 * âˆ‘' n : â„•+, Ïƒ 1 n * ğ•¢ z ^ (n : â„•))) := by
  have : -8 * Ï€ ^ 2 * âˆ‘' n : â„•+, Ïƒ 1 n * ğ•¢ z ^ (n : â„•) =
      âˆ‘' m : â„•, (-8 * Ï€ ^ 2 * âˆ‘' n : â„•+, n * ğ•¢ z ^ ((m + 1) * n)) := by
    have := tsum_prod_pow_eq_tsum_sigma 1 (norm_exp_two_pi_I_lt_one z)
    rw [tsum_pnat_eq_tsum_succ (f := fun d â†¦ âˆ‘' c : â„•+, c ^ 1 * ğ•¢ z ^ (d * c : â„•))] at this
    simp [â† tsum_mul_left, â† this]
  rw [this]
  refine (Summable.mul_left _ ?_).hasSum.comp tendsto_finset_range
  rw [â† summable_pnat_iff_summable_succ (f := fun b â†¦ âˆ‘' c : â„•+, c * ğ•¢ z ^ (b * c : â„•))]
  apply (summable_prod_mul_pow 1 (norm_exp_two_pi_I_lt_one z)).prod.congr
  simp [â† exp_nsmul]

lemma hasSum_e2Summand_symmetricIcc : HasSum (e2Summand Â· z)
    (2 * riemannZeta 2 - 8 * Ï€ ^ 2 * âˆ‘' n : â„•+, Ïƒ 1 n * ğ•¢ z ^ (n : â„•)) (symmetricIcc â„¤) := by
  simpa [HasSum, -symmetricIcc_filter, symmetricIcc_eq_map_Icc_nat, Function.comp_def,
    G2_partial_sum_eq] using (aux_G2_tendsto z).const_add _

lemma summable_e2Summand_symmetricIcc : Summable (e2Summand Â· z) (symmetricIcc â„¤) :=
  (hasSum_e2Summand_symmetricIcc z).summable

lemma G2_eq_tsum_cexp : G2 z = 2 * riemannZeta 2 - 8 * Ï€ ^ 2 * âˆ‘' n : â„•+, Ïƒ 1 n * ğ•¢ z ^ (n : â„•) :=
  (hasSum_e2Summand_symmetricIcc z).tsum_eq

lemma tendsto_e2Summand_atTop_nhds_zero : Tendsto (e2Summand Â· z) atTop (ğ“ 0) :=
  (summable_e2Summand_symmetricIcc z).tendsto_zero_of_even_summable_symmetricIcc (e2Summand_even _)

lemma hasSum_e2Summand_symmetricIco : HasSum (e2Summand Â· z)
    (2 * riemannZeta 2 - 8 * Ï€ ^ 2 * âˆ‘' n : â„•+, Ïƒ 1 n * ğ•¢ z ^ (n : â„•)) (symmetricIco â„¤) := by
  apply (hasSum_e2Summand_symmetricIcc z).hasSum_symmetricIco_of_hasSum_symmetricIcc
  simpa using (tendsto_e2Summand_atTop_nhds_zero z).neg.comp tendsto_natCast_atTop_atTop

lemma summable_e2Summand_symmetricIco : Summable (e2Summand Â· z) (symmetricIco â„¤) :=
  (hasSum_e2Summand_symmetricIco z).summable

lemma G2_eq_tsum_symmetricIco : G2 z = âˆ‘'[symmetricIco â„¤] m, e2Summand m z := by
  rw [G2, tsum_symmetricIcc_eq_tsum_symmetricIco (summable_e2Summand_symmetricIcc z)]
  simpa using (tendsto_e2Summand_atTop_nhds_zero z).neg.comp tendsto_natCast_atTop_atTop

section Auxiliary

open ModularGroup

variable (z : â„)

private lemma one_div_linear_sub_one_div_linear_eq (a b m : â„¤) (hm : m â‰  0 âˆ¨ (a â‰  0 âˆ§ b â‰  0)) :
    1 / ((m : â„‚) * z + a) - 1 / (m * z + b) = (b - a) * (1 / ((m * z + a) * (m * z + b))) := by
  rw [â† one_div_mul_sub_mul_one_div_eq_one_div_add_one_div]
  Â· grind [one_div, add_sub_add_left_eq_sub, mul_inv_rev]
  Â· simpa using linear_ne_zero z (cd := ![m, a]) (by aesop)
  Â· simpa using linear_ne_zero z (cd := ![m, b]) (by aesop)

lemma summable_left_one_div_linear_sub_one_div_linear (a b : â„¤) :
    Summable fun m : â„¤ â†¦ 1 / (m * (z : â„‚) + a) - 1 / (m * z + b) := by
  have := Summable.mul_left (b - a : â„‚) (summable_linear_left_mul_linear_left (ne_zero z) a b)
  rw [â† Finset.summable_compl_iff (s := {0})] at *
  apply this.congr (fun m â†¦ ?_)
  grind [one_div_linear_sub_one_div_linear_eq z a b m (by grind)]

lemma summable_right_one_div_linear_sub_one_div_linear_succ (m : â„¤) :
    Summable fun b : â„¤ â†¦ 1 / (m * (z : â„‚) + b) - 1 / (m * z + b + 1) := by
  have := summable_linear_right_add_one_mul_linear_right z m m
  rw [â† Finset.summable_compl_iff (s := {0, -1})] at *
  apply this.congr (fun b â†¦ ?_)
  simpa [add_assoc, mul_comm] using
    (one_div_linear_sub_one_div_linear_eq z b (b + 1) m (by grind)).symm

/- Acting by `S` (which sends `z` to `-z â»Â¹`) swaps the sums and pulls out a factor of
`(z ^ 2)â»Â¹`. -/
private lemma aux_sum_Ico_S_identity (N : â„•) :
    ((z : â„‚) ^ 2)â»Â¹ * (âˆ‘ x âˆˆ Ico (-N : â„¤) N, âˆ‘' (n : â„¤), (((x : â„‚) * (-â†‘z)â»Â¹ + n) ^ 2)â»Â¹) =
    âˆ‘' (n : â„¤), âˆ‘ x âˆˆ Ico (-N : â„¤) N, (((n : â„‚) * z + x) ^ 2)â»Â¹ := by
  simp_rw [inv_neg, mul_neg, mul_sum]
  rw [Summable.tsum_finsetSum (fun i hi â†¦ by apply linear_left_summable (ne_zero z) i le_rfl)]
  apply sum_congr rfl fun n hn â†¦ ?_
  rw [â† tsum_mul_left, â† tsum_comp_neg]
  apply tsum_congr (by grind [ne_zero z])

lemma tendsto_double_sum_S_act :
    Tendsto (fun N : â„• â†¦ (âˆ‘' (n : â„¤), âˆ‘ m âˆˆ Ico (-N : â„¤) N, (1 / ((n : â„‚) * z + m) ^ 2))) atTop
    (ğ“ ((z.1 ^ 2)â»Â¹ * G2 (S â€¢ z))) := by
  rw [G2_eq_tsum_symmetricIco, â† tsum_mul_left]
  have := ((summable_e2Summand_symmetricIco (S â€¢ z)).mul_left (z.1 ^ 2)â»Â¹).hasSum
  simp only [HasSum, symmetricIco, tendsto_map'_iff, modular_S_smul, â† Nat.map_cast_int_atTop] at *
  apply this.congr (fun N â†¦ ?_)
  simpa [e2Summand, eisSummand, â† mul_sum] using aux_sum_Ico_S_identity z N

lemma tsum_symmetricIco_tsum_eq_S_act :
    âˆ‘'[symmetricIco â„¤] n : â„¤, âˆ‘' m : â„¤, 1 / ((m : â„‚) * z + n) ^ 2 =
    ((z : â„‚) ^ 2)â»Â¹ * G2 (S â€¢ z) := by
  apply HasSum.tsum_eq
  rw [hasSum_symmetricIco_int_iff]
  apply (tendsto_double_sum_S_act z).congr (fun x â†¦ ?_)
  rw [Summable.tsum_finsetSum]
  exact fun i hi â†¦ by simpa using linear_left_summable (ne_zero z) i le_rfl

private lemma telescope_aux (z : â„‚) (m : â„¤) (b : â„•) :
    âˆ‘ n âˆˆ Ico (-b : â„¤) b, (1 / ((m : â„‚) * z + n) - 1 / (m * z + n + 1)) =
    1 / (m * z - b) - 1 / (m * z + b) := by
  convert sum_Ico_int_sub b (fun n â†¦ 1 / ((m : â„‚) * z + n)) using 2 <;>
  simp [add_assoc, sub_eq_add_neg]

lemma tsum_symmetricIco_linear_sub_linear_add_one_eq_zero (m : â„¤) :
    âˆ‘'[symmetricIco â„¤] n : â„¤, (1 / ((m : â„‚) * z + n) - 1 / (m * z + n + 1)) = 0 := by
  apply HasSum.tsum_eq
  simp_rw [hasSum_symmetricIco_int_iff, telescope_aux z m]
  simpa using (tendsto_zero_inv_linear_sub z m).sub (tendsto_zero_inv_linear z m)

/- We split the sum over `â„¤` into a sum over `â„•+` but of four terms.-/
private lemma aux_tsum_identity_1 (d : â„•+) :
    âˆ‘' (m : â„¤), (1 / ((m : â„‚) * z - d) - 1 / (m * z + d)) = -(2 / d) +
    âˆ‘' m : â„•+, (1 / ((m : â„‚) * z - d) + 1 / (-m * z + -d) - 1 / (m * z + d) -1 / (-m * z + d)) := by
  rw [eq_neg_add_iff_add_eq (b := 2 / (d : â„‚)), tsum_int_eq_zero_add_tsum_pnat]
  Â· simp only [Int.cast_zero, zero_mul, zero_sub, one_div, zero_add, Int.cast_natCast, Int.cast_neg,
      neg_mul]
    ring_nf
    rw [â† Summable.tsum_add]
    Â· grind
    Â· apply (summable_pnat_iff_summable_nat.mpr ((summable_int_iff_summable_nat_and_neg.mp
        (summable_left_one_div_linear_sub_one_div_linear z (-d) d)).1)).congr
      grind [Int.cast_natCast]
    Â· apply (summable_pnat_iff_summable_nat.mpr ((summable_int_iff_summable_nat_and_neg.mp
        (summable_left_one_div_linear_sub_one_div_linear z (-d) d)).2)).congr
      grind [Int.cast_neg, Int.cast_natCast, neg_mul, one_div]
  Â· simpa using summable_left_one_div_linear_sub_one_div_linear z (-d) d

/- The sum of four terms can now be combined into a sum where `z` has changed for `-1 / z`.-/
private lemma aux_tsum_identity_2 (d : â„•+) :
    âˆ‘' m : â„•+, (1 / ((m : â„‚) * z - d) + 1 / (-m * z + -d) - (1 / (m * z + d)) -
    1 / (-m * z + d)) = 2 / z * âˆ‘' m : â„•+, (1 / (-(d : â„‚) / z - m) + 1 / (-d / z + m)) := by
  rw [â† Summable.tsum_mul_left]
  Â· apply tsum_congr (by grind [sub_eq_add_neg, â† div_neg, ne_zero z])
  Â· have := summable_cotTerm (by simpa using z.int_div_mem_integerComplement (n := -d) (by aesop))
    simp only [cotTerm, one_div] at *
    simp only [â† Nat.cast_add_one] at this
    rw [summable_nat_add_iff (f := fun n â†¦ (-d / (z : â„‚) - n)â»Â¹ + (-d / (z : â„‚) + n)â»Â¹)] at this
    apply this.subtype

private lemma aux_tendsto_tsum_cexp_pnat :
    Tendsto (fun N : â„•+ â†¦ âˆ‘' (n : â„•+), cexp (2 * Ï€ * I * (-N / z)) ^ (n : â„•)) atTop (ğ“ 0) := by
  have := tendsto_zero_geometric_tsum_pnat (norm_exp_two_pi_I_lt_one âŸ¨_, im_pnat_div_pos 1 zâŸ©)
  simp only [coe_mk_subtype, â† exp_nsmul, nsmul_eq_mul, Nat.cast_mul] at *
  exact this.congr <| by grind

/- Now this sum of terms with `-1 / z` tendsto `-2 * Ï€ * I / z` which is exactly `D2_S`. The key is
to use the cotangent series to write this as a sum of exponentials.-/
private lemma aux_tendsto_tsum : Tendsto (fun n : â„• â†¦ 2 / z *
    âˆ‘' (m : â„•+), (1 / (-(n : â„‚) / z - m) + 1 / (-n / z + m))) atTop (ğ“ (-2 * Ï€ * I / z)) := by
  rw [â† PNat.tendsto_comp_val_iff]
  have H0 : (fun n : â„•+ â†¦ (2 / z * âˆ‘' (m : â„•+), (1 / (-(n : â„‚) / z - m) + 1 / (-n / z + m)))) =
      (fun n : â„•+ â†¦ (-2 * Ï€ * I / z) - (2 / z * (2 * Ï€ * I)) *
      (âˆ‘' m : â„•+, cexp (2 * Ï€ * I * (-n / z)) ^ (m : â„•)) + 2 / n) := by
    ext N
    have h2 := cot_series_rep (coe_mem_integerComplement âŸ¨-N / z, im_pnat_div_pos N zâŸ©)
    rw [pi_mul_cot_pi_q_exp, â† sub_eq_iff_eq_add',coe_mk_subtype, one_div, inv_div, neg_mul, â† h2,
      â† tsum_zero_pnat_eq_tsum_nat
      (by simpa using norm_exp_two_pi_I_lt_one âŸ¨-N / z, im_pnat_div_pos N zâŸ©)] at *
    field [ne_zero z]
  rw [H0]
  nth_rw 2 [show -2 * Ï€ * I / z = (-2 * Ï€ * I / z) - (2 / z * (2 * Ï€ * I)) * 0 + 2 * 0 by ring]
  refine aux_tendsto_tsum_cexp_pnat z |>.const_mul _ |>.const_sub _ |>.add (.const_mul _ ?_)
  exact PNat.tendsto_comp_val_iff.mpr tendsto_inv_atTop_nhds_zero_nat

/- This shows that the limit of the conditional sum over larger intervals tends
to `-2 * Ï€ * I / z`. We will then show, in `tsum_tsum_symmetricIco_sub_eq` that if we swap the
order of the sum it tends to `0` instead. -/
lemma tendsto_tsum_one_div_linear_sub_succ_eq :
    Tendsto (fun N : â„•+ â†¦ âˆ‘ n âˆˆ Ico (-N : â„¤) N,
    âˆ‘' m : â„¤, (1 / ((m : â„‚) * z + n) - 1 / (m * z + n + 1))) atTop (ğ“ (-2 * Ï€ * I / z)) := by
  have (N : â„•+) :
      âˆ‘ n âˆˆ Ico (-N : â„¤) N, âˆ‘' m : â„¤ , (1 / ((m : â„‚) * z + n) - 1 / (m * z + n + 1))
      = âˆ‘' m : â„¤ , âˆ‘ n âˆˆ Ico (-N : â„¤) N, (1 / ((m : â„‚) * z + n) - 1 / (m * z + n + 1)) := by
    rw [Summable.tsum_finsetSum (fun i hi â†¦ ?_)]
    apply (summable_left_one_div_linear_sub_one_div_linear z i (i + 1)).congr
    grind
  simp only [telescope_aux, aux_tsum_identity_1] at this
  rw [funext this, show -2 * Ï€ * I / z = 0 + -2 * Ï€ * I / z by ring]
  apply Tendsto.add
  Â· simpa [â† PNat.tendsto_comp_val_iff] using
      (tendsto_inv_atTop_nhds_zero_nat (ğ•œ := â„‚)).const_mul (-2)
  Â· simpa only [aux_tsum_identity_2, â† PNat.tendsto_comp_val_iff] using aux_tendsto_tsum z

/- These are the two key lemmas, which show that swapping the order of summation gives
results differing by the term `-2 * Ï€ * I / z`. -/
lemma tsum_symmetricIco_tsum_sub_eq :
    âˆ‘'[symmetricIco â„¤] n : â„¤, âˆ‘' m : â„¤, (1 / ((m : â„‚) * z + n) - 1 / (m * z + n + 1)) =
    -2 * Ï€ * I / z := by
  apply HasSum.tsum_eq
  simpa [HasSum, â† Nat.map_cast_int_atTop, â† PNat.tendsto_comp_val_iff]
    using tendsto_tsum_one_div_linear_sub_succ_eq z

lemma tsum_tsum_symmetricIco_sub_eq :
    âˆ‘' m : â„¤, âˆ‘'[symmetricIco â„¤] n : â„¤, (1 / ((m : â„‚) * z + n) - 1 / (m * z + n + 1)) = 0 := by
  convert tsum_zero
  exact tsum_symmetricIco_linear_sub_linear_add_one_eq_zero z _

end Auxiliary

end EisensteinSeries
