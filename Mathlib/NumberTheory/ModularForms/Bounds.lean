/-
Copyright (c) 2025 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
module

public import Mathlib.NumberTheory.Modular
public import Mathlib.NumberTheory.ModularForms.Petersson

/-!
# Bounds for the norm of a modular form

We prove bounds for the norm of a modular form `f Ï„` in terms of `im Ï„`, and deduce polynomial
bounds for its q-expansion coefficients. The main results are

* `ModularFormClass.exists_bound`: a modular form of weight `k` (for an arithmetic subgroup `Î“`)
  is bounded by a constant multiple of `max 1 (1 / (im Ï„) ^ k))`.
* `CuspFormClass.exists_bound`: a cusp form of weight `k` (for an arithmetic subgroup `Î“`)
  is bounded by a constant multiple of `1 / (im Ï„) ^ (k / 2)`.
* `ModularFormClass.qExpansion_isBigO`: for a a modular form of weight `k` (for an arithmetic
  subgroup `Î“`), the `n`-th q-expansion coefficient is `O(n ^ k)`.
* `CuspFormClass.qExpansion_isBigO`: **Hecke's bound** for a a cusp form of weight `k` (for
  an arithmetic subgroup `Î“`): the `n`-th q-expansion coefficient is `O(n ^ (k / 2))`.
-/
public section

open Filter Topology Asymptotics Matrix.SpecialLinearGroup Matrix.GeneralLinearGroup

open UpperHalfPlane hiding I

open Matrix hiding mul_smul

open scoped Modular MatrixGroups ComplexConjugate ModularForm

variable {E : Type*} [SeminormedAddCommGroup E]

namespace ModularGroup

lemma exists_bound_fundamental_domain_of_isBigO
    {f : â„ â†’ E} (hf_cont : Continuous f) {t : â„} (hf_infinity : f =O[atImInfty] fun z â†¦ z.im ^ t) :
    âˆƒ F, âˆ€ Ï„ âˆˆ ğ’Ÿ, â€–f Ï„â€– â‰¤ F * Ï„.im ^ t := by
  -- Extract a bound for large `im Ï„` using `hf_infty`.
  obtain âŸ¨D, hD, hf_infinityâŸ© := hf_infinity.exists_pos
  rw [IsBigOWith, atImInfty, eventually_comap, eventually_atTop] at hf_infinity
  obtain âŸ¨y, hyâŸ© := hf_infinity
  simp only [Real.norm_rpow_of_nonneg (_ : â„).im_pos.le,
      Real.norm_of_nonneg (_ : â„).im_pos.le] at hy
  -- Extract a bound for the rest of `ğ’Ÿ` using continuity and compactness.
  have hfm : ContinuousOn (fun Ï„ â†¦ â€–f Ï„â€– / (im Ï„) ^ t) (truncatedFundamentalDomain y) := by
    apply (hf_cont.norm.div ?_ fun Ï„ â†¦ by positivity).continuousOn
    exact continuous_im.rpow_const fun Ï„ â†¦ .inl Ï„.im_ne_zero
  obtain âŸ¨E, hEâŸ© : âˆƒ E, âˆ€ Ï„ âˆˆ truncatedFundamentalDomain y, â€–f Ï„â€– / (im Ï„) ^ t â‰¤ E := by
    simpa [norm_mul, norm_norm, Real.norm_rpow_of_nonneg (_ : â„).im_pos.le,
      Real.norm_of_nonneg (_ : â„).im_pos.le]
      using (isCompact_truncatedFundamentalDomain y).exists_bound_of_continuousOn hfm
  -- Put the two bounds together.
  refine âŸ¨max D E, fun Ï„ hÏ„ â†¦ ?_âŸ©
  rcases le_total y (im Ï„) with hÏ„' | hÏ„'
  Â· exact (hy _ hÏ„' _ rfl).trans <| mul_le_mul_of_nonneg_right (le_max_left ..) (by positivity)
  Â· rw [â† div_le_iffâ‚€ (by positivity)]
    exact (hE Ï„ âŸ¨hÏ„, hÏ„'âŸ©).trans (le_max_right _ _)

/-- A function on `â„` which is invariant under `SL(2, â„¤)`, and is `O ((im Ï„) ^ t)` at `Iâˆ` for
some `0 â‰¤ t`, is bounded on `â„` by a constant multiple of `(max (im Ï„) (1 / im Ï„)) ^ t`.

This will be applied to `f Ï„ * (im Ï„) ^ (k / 2)` for `f` a modular form of weight `k`, taking
`t = 0` if `f` is cuspidal, and `t = k / 2` otherwise. -/
lemma exists_bound_of_invariant_of_isBigO {f : â„ â†’ E} (hf_cont : Continuous f) {t : â„} (ht : 0 â‰¤ t)
    (hf_infinity : f =O[atImInfty] fun z â†¦ (im z) ^ t)
    (hf_inv : âˆ€ (g : SL(2, â„¤)) Ï„, f (g â€¢ Ï„) = f Ï„) :
    âˆƒ C, âˆ€ Ï„, â€–f Ï„â€– â‰¤ C * (max (im Ï„) (1 / im Ï„)) ^ t := by
  -- First find an `F` such that `âˆ€ Ï„ âˆˆ ğ’Ÿ, â€–f Ï„â€– â‰¤ F * Ï„.im ^ t`.
  obtain âŸ¨F, hFğ’ŸâŸ© : âˆƒ F, âˆ€ Ï„ âˆˆ ğ’Ÿ, â€–f Ï„â€– â‰¤ F * Ï„.im ^ t :=
    exists_bound_fundamental_domain_of_isBigO hf_cont hf_infinity
  refine âŸ¨F, fun Ï„ â†¦ ?_âŸ©
  -- Given `Ï„`, choose a `g = [a, b; c, d] âˆˆ SL(2, â„¤)` that translates `Ï„` into `ğ’Ÿ`.
  obtain âŸ¨g, hgâŸ© := exists_smul_mem_fd Ï„
  specialize hFğ’Ÿ (g â€¢ Ï„) hg
  rw [hf_inv g Ï„] at hFğ’Ÿ
  grw [hFğ’Ÿ]
  gcongr
  Â· rw [â† div_le_iffâ‚€ (by positivity)] at hFğ’Ÿ
    exact le_trans (by positivity) hFğ’Ÿ
  -- It remains to show `(g â€¢ Ï„).im â‰¤ max Ï„.im (1 / Ï„.im)`.
  -- We split into two cases depending whether `c = g 1 0` is zero.
  rw [im_smul_eq_div_normSq, denom_apply]
  by_cases hg : g 1 0 = 0
  Â· -- If `c = 0`, then `(g â€¢ Ï„).im = Ï„.im / d ^ 2` and `d ^ 2 â‰¥ 1`.
    -- (In fact `d = Â±1`, but we do not need this stronger statement).
    have : g 1 1 â‰  0 := fun hg' â†¦ zero_ne_one <| by
      simpa only [Matrix.det_fin_two, hg, hg', mul_zero, mul_zero, sub_zero] using g.det_coe
    have : (1 : â„) â‰¤ g 1 1 ^ 2 := mod_cast (one_le_sq_iff_one_le_abs _).mpr (Int.one_le_abs this)
    refine le_trans ?_ <| le_max_left _ _
    rw [show Complex.normSq ((g 1 0) * Ï„ + (g 1 1)) = (g 1 1) ^ 2 by simp [hg, sq]]
    simpa [field] using inv_le_one_of_one_leâ‚€ this
  Â· -- If `c â‰  0`, then `1 â‰¤ c ^ 2`, so
    -- `(g â€¢ Ï„).im = Ï„.im / (c ^ 2 * Ï„.im ^ 2 +  ...) â‰¤ 1 / Ï„.im`.
    refine le_trans ?_ <| le_max_right _ _
    rw [show 1 / Ï„.im = Ï„.im / Ï„.im ^ 2 by field_simp]
    gcongr
    rw [show Complex.normSq ((g 1 0) * Ï„ + (g 1 1)) =
      ((g 1 0) * Ï„.re + (g 1 1)) ^ 2 + (g 1 0) ^ 2 * Ï„.im ^ 2 by simp [Complex.normSq_apply]; ring]
    have : (1 : â„) â‰¤ g 1 0 ^ 2 := mod_cast (one_le_sq_iff_one_le_abs _).mpr (Int.one_le_abs hg)
    nlinarith

/-- A function on `â„` which is invariant under a finite-index subgroup of `SL(2, â„¤)`, and satisfies
an `O((im Ï„) ^ t)` bound at all cusps for some `0 â‰¤ t`, is in fact uniformly bounded by a multiple
of `(max (im Ï„) (1 / im Ï„)) ^ t`. -/
lemma exists_bound_of_subgroup_invariant_of_isBigO
    {f : â„ â†’ E} (hf_cont : Continuous f) {t : â„} (ht : 0 â‰¤ t)
    (hf_infinity : âˆ€ (g : SL(2, â„¤)), (fun Ï„ â†¦ f (g â€¢ Ï„)) =O[atImInfty] fun z â†¦ (im z) ^ t)
    {Î“ : Subgroup SL(2, â„¤)} [Î“.FiniteIndex] (hf_inv : âˆ€ g âˆˆ Î“, âˆ€ Ï„, f (g â€¢ Ï„) = f Ï„) :
    âˆƒ C, âˆ€ Ï„, â€–f Ï„â€– â‰¤ C * max Ï„.im (1 / Ï„.im) ^ t := by
  -- marshall the info we have in terms of a function on the quotient
  let f' Ï„ : SL(2, â„¤) â§¸ Î“ â†’ E := Quotient.lift (fun g â†¦ f (gâ»Â¹ â€¢ Ï„)) fun g h hgh â†¦ by
    obtain âŸ¨j, hj, hj'âŸ© : âˆƒ j âˆˆ Î“, h = g * j := by
      rw [â† Quotient.eq_iff_equiv, Quotient.eq, QuotientGroup.leftRel_apply] at hgh
      exact âŸ¨gâ»Â¹ * h, hgh, (mul_inv_cancel_left g h).symmâŸ©
    simp [-sl_moeb, hj', mul_smul, hf_inv jâ»Â¹ (inv_mem hj)]
  have hf'_cont Î³ : Continuous (f' Â· Î³) := QuotientGroup.induction_on Î³ fun g â†¦ by
    simp only [sl_moeb, Quotient.lift_mk, f']
    fun_prop
  have hf'_inv Ï„ (g : SL(2, â„¤)) Î³ : f' (g â€¢ Ï„) (g â€¢ Î³) = f' Ï„ Î³ := by
    induction Î³ using QuotientGroup.induction_on
    simp [-sl_moeb, f', mul_smul]
  have hf'_infty Î³ : (f' Â· Î³) =O[_] _ := Î³.induction_on fun h â†¦ hf_infinity hâ»Â¹
  -- now take the sum over the quotient
  have : Fintype (SL(2, â„¤) â§¸ Î“) := Subgroup.fintypeQuotientOfFiniteIndex
  -- Now the conclusion is very simple.
  obtain âŸ¨C, hCâŸ© := exists_bound_of_invariant_of_isBigO (by fun_prop) ht
    (.sum fun i _ â†¦ (hf'_infty i).norm_left)
    (fun g Ï„ â†¦ (Fintype.sum_equiv (MulAction.toPerm g) _ _ (by simp [-sl_moeb, hf'_inv])).symm)
  refine âŸ¨C, fun Ï„ â†¦ le_trans ?_ (hC Ï„)âŸ©
  simpa [Real.norm_of_nonneg <| show 0 â‰¤ âˆ‘ Î³, â€–f' Ï„ Î³â€– by positivity, -sl_moeb, f'] using
    Finset.univ.single_le_sum (fun Î³ _ â†¦ norm_nonneg (f' Ï„ Î³)) (Finset.mem_univ âŸ¦1âŸ§)

/-- A function on `â„` which is invariant under an arithmetic subgroup of `GL(2, â„)`, and satisfies
an `O((im Ï„) ^ t)` bound at all cusps for some `0 â‰¤ t`, is in fact uniformly bounded by a multiple
of `(max (im Ï„) (1 / im Ï„)) ^ t`. -/
lemma exists_bound_of_subgroup_invariant_of_isArithmetic_of_isBigO
    {f : â„ â†’ E} (hf_cont : Continuous f) {t : â„} (ht : 0 â‰¤ t)
    (hf_infinity : âˆ€ (g : SL(2, â„¤)), (fun Ï„ â†¦ f (g â€¢ Ï„)) =O[atImInfty] fun z â†¦ (im z) ^ t)
    {Î“ : Subgroup (GL (Fin 2) â„)} [Î“.IsArithmetic] (hf_inv : âˆ€ g âˆˆ Î“, âˆ€ Ï„, f (g â€¢ Ï„) = f Ï„) :
    âˆƒ C, âˆ€ Ï„, â€–f Ï„â€– â‰¤ C * max Ï„.im (1 / Ï„.im) ^ t :=
  exists_bound_of_subgroup_invariant_of_isBigO hf_cont ht hf_infinity (Î“ := Î“.comap (mapGL â„))
    (hf_inv Â·)

/-- A function on `â„` which is invariant under `SL(2, â„¤)`, and bounded at `âˆ`, is uniformly
bounded. -/
lemma exists_bound_of_invariant
    {f : â„ â†’ E} (hf_cont : Continuous f) (hf_infinity : IsBoundedAtImInfty f)
    (hf_inv : âˆ€ (g : SL(2, â„¤)) Ï„, f (g â€¢ Ï„) = f Ï„) :
    âˆƒ C, âˆ€ Ï„, â€–f Ï„â€– â‰¤ C := by
  simpa using exists_bound_of_invariant_of_isBigO hf_cont le_rfl
    (by simpa only [Real.rpow_zero] using hf_infinity) hf_inv

/-- A function on `â„` which is invariant under an arithmetic subgroup and bounded at all cusps,
is uniformly bounded. -/
lemma exists_bound_of_subgroup_invariant {f : â„ â†’ E} (hf_cont : Continuous f)
    (hf_infinity : âˆ€ (g : SL(2, â„¤)), IsBoundedAtImInfty fun Ï„ â†¦ f (g â€¢ Ï„))
    {Î“ : Subgroup (GL (Fin 2) â„)} [Î“.IsArithmetic] (hf_inv : âˆ€ g âˆˆ Î“, âˆ€ Ï„, f (g â€¢ Ï„) = f Ï„) :
    âˆƒ C, âˆ€ Ï„, â€–f Ï„â€– â‰¤ C := by
  simpa using exists_bound_of_subgroup_invariant_of_isArithmetic_of_isBigO hf_cont le_rfl
    (by simpa only [Real.rpow_zero] using hf_infinity) hf_inv

end ModularGroup

/-- If `f, f'` are modular forms, then `petersson k f f'` is bounded by a constant multiple of
`max Ï„.im (1 / Ï„.im) ^ k`. -/
lemma ModularFormClass.exists_petersson_le {k : â„¤} (hk : 0 â‰¤ k) (Î“ : Subgroup (GL (Fin 2) â„))
    [Î“.IsArithmetic] {F F' : Type*} (f : F) (f' : F')
    [FunLike F â„ â„‚] [FunLike F' â„ â„‚] [ModularFormClass F Î“ k] [ModularFormClass F' Î“ k] :
    âˆƒ C, âˆ€ Ï„, â€–petersson k f f' Ï„â€– â‰¤ C * max Ï„.im (1 / Ï„.im) ^ k := by
  conv => enter [1, C, Ï„, 1]; rw [â† norm_norm]
  refine mod_cast ModularGroup.exists_bound_of_subgroup_invariant_of_isArithmetic_of_isBigO
    (show Continuous (â€–petersson k f f' Â·â€–) by fun_prop) (mod_cast hk : 0 â‰¤ (k : â„))
    (fun g â†¦ ?_) (fun g hg Ï„ â†¦ SlashInvariantFormClass.norm_petersson_smul hg)
  simp_rw [â† UpperHalfPlane.petersson_slash_SL, Real.rpow_intCast]
  simpa [petersson, Real.norm_of_nonneg (_ : â„).im_pos.le]
    using (bdd_at_infty_slash f g).norm_left.mul (bdd_at_infty_slash f' g).norm_left
      |>.mul (isBigO_refl ..)

open ConjAct Pointwise in
/-- If `f` is a cusp form and `f'` a modular form, then `petersson k f f'` is bounded. -/
lemma CuspFormClass.petersson_bounded_left
    (k : â„¤) (Î“ : Subgroup (GL (Fin 2) â„)) [Î“.IsArithmetic] {F F' : Type*} (f : F) (f' : F')
    [FunLike F â„ â„‚] [FunLike F' â„ â„‚] [CuspFormClass F Î“ k] [ModularFormClass F' Î“ k] :
    âˆƒ C, âˆ€ Ï„, â€–petersson k f f' Ï„â€– â‰¤ C := by
  conv => enter [1, C, Ï„, 1]; rw [â† norm_norm]
  refine ModularGroup.exists_bound_of_subgroup_invariant (by fun_prop) (fun g â†¦ ?_)
    fun g hg Ï„ â†¦ SlashInvariantFormClass.norm_petersson_smul hg
  apply IsZeroAtImInfty.isBoundedAtImInfty
  rw [IsZeroAtImInfty, ZeroAtFilter, â† tendsto_zero_iff_norm_tendsto_zero]
  simp_rw [â† UpperHalfPlane.petersson_slash_SL]
  have : ((toConjAct (g : GL (Fin 2) â„)â»Â¹) â€¢ Î“).IsArithmetic := by
    simpa [(show Rat.castHom â„ = algebraMap â„š â„ by rfl), map_inv, map_mapGL]
      using Subgroup.IsArithmetic.conj Î“ (mapGL â„š g)â»Â¹
  exact (zero_at_infty <| CuspForm.translate f g).petersson_isZeroAtImInfty_left k _
    (ModularForm.translate f' g)

/-- If `f` is a modular form and `f'` a cusp form, then `petersson k f f'` is bounded. -/
lemma CuspFormClass.petersson_bounded_right
    (k : â„¤) (Î“ : Subgroup (GL (Fin 2) â„)) [Î“.IsArithmetic] {F F' : Type*} (f : F) (f' : F')
    [FunLike F â„ â„‚] [FunLike F' â„ â„‚] [ModularFormClass F Î“ k] [CuspFormClass F' Î“ k] :
    âˆƒ C, âˆ€ Ï„, â€–petersson k f f' Ï„â€– â‰¤ C := by
  simpa [petersson_norm_symm] using petersson_bounded_left k Î“ f' f

/-- A weight `k` cusp form is bounded in norm by a constant multiple of `(im Ï„) ^ (-k / 2)`. -/
lemma CuspFormClass.exists_bound {k : â„¤} {Î“ : Subgroup (GL (Fin 2) â„)} [Î“.IsArithmetic]
    {F : Type*} [FunLike F â„ â„‚] [CuspFormClass F Î“ k] (f : F) :
    âˆƒ C, âˆ€ Ï„, â€–f Ï„â€– â‰¤ C / Ï„.im ^ (k / 2 : â„) := by
  obtain âŸ¨C, hCâŸ© := petersson_bounded_left k Î“ f f
  refine âŸ¨C.sqrt, fun Ï„ â†¦ ?_âŸ©
  specialize hC Ï„
  rw [â† sq_le_sqâ‚€ (by positivity) (by positivity), div_pow, Real.sq_sqrt ((norm_nonneg _).trans hC)]
  grw [â† hC]
  rw [petersson, â† Real.rpow_mul_natCast Ï„.im_pos.le]
  simp [abs_of_pos Ï„.im_pos, field]

open Real in
/-- A weight `k` modular form is bounded in norm by a constant multiple of
`max 1 (1 / (Ï„.im) ^ k)`. -/
lemma ModularFormClass.exists_bound {k : â„¤} (hk : 0 â‰¤ k) {Î“ : Subgroup (GL (Fin 2) â„)}
    [Î“.IsArithmetic] {F : Type*} [FunLike F â„ â„‚] [ModularFormClass F Î“ k] (f : F) :
    âˆƒ C, âˆ€ Ï„, â€–f Ï„â€– â‰¤ C * (max 1 (1 / (Ï„.im) ^ k)) := by
  obtain âŸ¨C, hCâŸ© := ModularFormClass.exists_petersson_le hk Î“ f f
  refine âŸ¨C.sqrt, fun Ï„ â†¦ ?_âŸ©
  lift k to â„• using hk
  specialize hC Ï„
  have hC' : 0 â‰¤ C := le_trans (by positivity) <| (div_le_iffâ‚€ (by positivity)).mpr hC
  have h : 0 < â€–(Ï„.im : â„‚) ^ (k : â„¤)â€– := mod_cast norm_pos_iff.mpr (pow_ne_zero _ Ï„.im_ne_zero)
  rw [petersson, norm_mul, norm_mul, Complex.norm_conj, â† sq, â† le_div_iffâ‚€ h, mul_div_assoc] at hC
  rw [â† sq_le_sqâ‚€ (by positivity) (by positivity), mul_pow, sq_sqrt hC']
  refine hC.trans (congrArg (C * Â·) ?_).le
  -- remains to show `(max Ï„.im (1 / Ï„.im)) ^ k / â€–Ï„.im ^ kâ€– = (max 1 (1 / Ï„.im ^ k)) ^ 2`,
  -- which is easier after lifting to `NNReal`
  generalize h : Ï„.im = t
  have ht : 0 < t := h â–¸ Ï„.im_pos
  lift t to NNReal using ht.le
  rw [â† coe_nnnorm]
  norm_cast at âŠ¢ ht
  rw [(pow_left_mono k).map_max, (pow_left_mono 2).map_max, â† max_div_div_right (by positivity)]
  congr <;> simp [field, ht.ne']

local notation "ğ•¢" => Function.Periodic.qParam

open Complex ModularFormClass

/-- General result on bounding q-expansion coefficients using a bound on the norm of the function.
This will get used twice over, once for cusp forms (with `e = k / 2`) and once for modular forms
(with `e = k`). -/
lemma qExpansion_coeff_isBigO_of_norm_isBigO {k : â„¤} {Î“ : Subgroup (GL (Fin 2) â„)}
    [Î“.IsArithmetic] {F : Type*} [FunLike F â„ â„‚] [ModularFormClass F Î“ k] (f : F) (e : â„)
    (hF : IsBigO (comap UpperHalfPlane.im (ğ“ 0)) f (fun Ï„ â†¦ Ï„.im ^ (-e))) :
    (fun n â†¦ (qExpansion Î“.strictWidthInfty f).coeff n) =O[atTop] fun n â†¦ (n : â„) ^ e := by
  let h := Î“.strictWidthInfty
  have hh : 0 < h := Î“.strictWidthInfty_pos_iff.mpr Fact.out
  have : NeZero h := âŸ¨hh.ne'âŸ©
  have hÎ“ : h âˆˆ Î“.strictPeriods := Î“.strictWidthInfty_mem_strictPeriods
  obtain âŸ¨C, Cpos, hCâŸ© := hF.exists_pos
  rw [isBigO_iff]
  rw [IsBigOWith, eventually_comap] at hC
  use (1 / Real.exp (-2 * Real.pi / â†‘h)) * C
  filter_upwards [eventually_gt_atTop 0,
    (tendsto_inv_atTop_zero.comp tendsto_natCast_atTop_atTop).eventually hC] with n hn hn'
  rw [qExpansion_coeff_eq_intervalIntegral (t := 1 / n) f hh hÎ“ _ (by positivity),
    â† intervalIntegral.integral_const_mul]
  simp only [ofReal_div, ofReal_one, ofReal_natCast]
  refine intervalIntegral.norm_integral_le_integral_norm (by positivity) |>.trans ?_
  let F (x : â„) : â„ := â€–1 / â†‘h * (1 / ğ•¢ h ((x : â„‚) + 1 / n * I) ^ n
      * f âŸ¨(x : â„‚) + 1 / n * Complex.I, by simp [hn]âŸ©)â€–
  have hne : â€–(n : â„) ^ eâ€– = n ^ e := Real.norm_of_nonneg (by positivity)
  have (x : â„) : F x â‰¤ 1 / h * (1 / Real.exp (-2 * Real.pi / â†‘h)) * (C * n ^ e) := by
    simp only [F, norm_mul, norm_div, norm_real, norm_one, norm_pow, mul_assoc]
    rw [Real.norm_of_nonneg hh.le, Function.Periodic.norm_qParam, â† Real.exp_nat_mul]
    gcongr
    Â· simp [field]
    Â· grw [hn' _ (by simp [â† UpperHalfPlane.coe_im])]
      simp [â† UpperHalfPlane.coe_im, Real.rpow_neg_eq_inv_rpow, hne]
  refine (intervalIntegral.integral_mono (by positivity) ?_ ?_ this).trans (le_of_eq ?_)
  Â· refine continuous_const.mul (.mul ?_ ?_) |>.norm |>.intervalIntegrable _ _
    Â· simp only [Function.Periodic.qParam, â† Complex.exp_nat_mul, one_div, â† Complex.exp_neg]
      fun_prop
    Â· exact (continuous f).comp (by fun_prop)
  Â· exact continuous_const.intervalIntegrable ..
  Â· simp [field, intervalIntegral.integral_const, hne]

/-- Bound for the coefficients of a modular form: if `f` is a weight `k` modular form for an
arithmetic subgroup, then its `q`-expansion coefficients are `O (n ^ k)`.

This is not optimal -- the optimal exponent is `k - 1 + Îµ` for any `0 < Îµ`, at least for congruence
levels -- but is much easier to prove than the optimal result.

See `CuspFormClass.qExpansion_isBigO` for a sharper bound assuming `f` is cuspidal. -/
lemma ModularFormClass.qExpansion_isBigO {k : â„¤} (hk : 0 â‰¤ k) {Î“ : Subgroup (GL (Fin 2) â„)}
    [Î“.IsArithmetic] {F : Type*} [FunLike F â„ â„‚] [ModularFormClass F Î“ k] (f : F) :
    (fun n â†¦ (qExpansion Î“.strictWidthInfty f).coeff n) =O[atTop] fun n â†¦ (n : â„) ^ k := by
  simp only [â† Real.rpow_intCast]
  apply qExpansion_coeff_isBigO_of_norm_isBigO
  obtain âŸ¨C, hCâŸ© := exists_bound hk f
  simp_rw [IsBigO, â† Int.cast_neg, Real.rpow_intCast, IsBigOWith, eventually_comap]
  use C
  filter_upwards [eventually_le_nhds zero_lt_one] with _ hÏ„ Ï„ rfl
  refine (hC Ï„).trans (le_of_eq ?_)
  rw [max_eq_right, zpow_neg, Real.norm_of_nonneg (by positivity), one_div]
  exact one_le_one_div (by positivity) (zpow_le_oneâ‚€ Ï„.im_pos hÏ„ hk)

/-- **Hecke's bound** for the coefficients of a cusp form: if `f` is a weight `k` modular form for
an arithmetic subgroup, then its `q`-expansion coefficients are `O (n ^ (k / 2))`.

This is not optimal -- the optimal exponent is `(k - 1) / 2 + Îµ` for any `0 < Îµ`, at least for
congruence levels -- but is much easier to prove than the optimal result. -/
lemma CuspFormClass.qExpansion_isBigO {k : â„¤} {Î“ : Subgroup (GL (Fin 2) â„)}
    [Î“.IsArithmetic] {F : Type*} [FunLike F â„ â„‚] [CuspFormClass F Î“ k] (f : F) :
    (fun n â†¦ (ModularFormClass.qExpansion Î“.strictWidthInfty f).coeff n)
      =O[atTop] fun n â†¦ (n : â„) ^ ((k : â„) / 2) := by
  apply qExpansion_coeff_isBigO_of_norm_isBigO
  obtain âŸ¨C, hCâŸ© := exists_bound f
  refine isBigO_of_le' (c := C) _ fun Ï„ â†¦ (hC Ï„).trans (of_eq ?_)
  rw [Real.norm_of_nonneg (by positivity), Real.rpow_neg Ï„.im_pos.le, div_eq_mul_inv]
