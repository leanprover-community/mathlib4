/-
Copyright (c) 2024 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/
import Mathlib.Analysis.Complex.AbsMax
import Mathlib.NumberTheory.Modular
import Mathlib.NumberTheory.ModularForms.QExpansion
/-!
# Level one modular forms

This file contains results specific to modular forms of level one, i.e. modular forms for
`SL(2, ‚Ñ§)`.

TODO: Add finite-dimensionality of these spaces of modular forms.

-/

open UpperHalfPlane ModularGroup SlashInvariantForm ModularForm Complex
  CongruenceSubgroup Real Function SlashInvariantFormClass ModularFormClass Periodic
  Matrix.SpecialLinearGroup

local notation "ùï¢" => qParam

variable {F : Type*} [FunLike F ‚Ñç ‚ÑÇ] {k : ‚Ñ§}

namespace SlashInvariantForm

variable [SlashInvariantFormClass F (Œì(1).map <| mapGL ‚Ñù) k]

lemma exists_one_half_le_im_and_norm_le (hk : k ‚â§ 0) (f : F) (œÑ : ‚Ñç) :
    ‚àÉ Œæ : ‚Ñç, 1 / 2 ‚â§ Œæ.im ‚àß ‚Äñf œÑ‚Äñ ‚â§ ‚Äñf Œæ‚Äñ :=
  let ‚ü®Œ≥, hŒ≥, hdenom‚ü© := exists_one_half_le_im_smul_and_norm_denom_le œÑ
  ‚ü®Œ≥ ‚Ä¢ œÑ, hŒ≥, by
    simpa only [MulAction.compHom_smul_def, norm_mul, norm_zpow,
      slash_action_eqn'' _ (Subgroup.mem_map_of_mem (mapGL ‚Ñù) (mem_Gamma_one Œ≥))] using
      le_mul_of_one_le_left (norm_nonneg _) <|
        one_le_zpow_of_nonpos‚ÇÄ (norm_pos_iff.2 (denom_ne_zero _ _)) hdenom hk‚ü©

variable (k) in
/-- If a constant function is modular of weight `k`, then either `k = 0`, or the constant is `0`. -/
lemma wt_eq_zero_of_eq_const {f : F} {c : ‚ÑÇ} (hf : ‚áëf = Function.const _ c) :
    k = 0 ‚à® c = 0 := by
  have Smem := Subgroup.mem_map_of_mem (mapGL ‚Ñù) (mem_Gamma_one S)
  have hI := slash_action_eqn'' f Smem I
  have h2I2 := slash_action_eqn'' f Smem ‚ü®2 * Complex.I, by norm_num‚ü©
  simp_rw [hf, Function.const, mapGL, MonoidHom.comp_apply, algebraMap_int_eq, denom_S,
    coe_mk_subtype] at hI h2I2
  nth_rw 1 [h2I2] at hI
  simp only [mul_zpow, coe_I, mul_eq_mul_right_iff, mul_left_eq_self‚ÇÄ] at hI
  refine hI.imp_left (Or.casesOn ¬∑ (fun H ‚Ü¶ ?_) (False.elim ‚àò zpow_ne_zero k I_ne_zero))
  rwa [‚Üê ofReal_ofNat, ‚Üê ofReal_zpow, ‚Üê ofReal_one, ofReal_inj,
    zpow_eq_one_iff_right‚ÇÄ (by norm_num) (by norm_num)] at H

end SlashInvariantForm

namespace ModularFormClass

variable [ModularFormClass F (Œì(1).map <| mapGL ‚Ñù) k]

private theorem cuspFunction_eqOn_const_of_nonpos_wt (hk : k ‚â§ 0) (f : F) :
    Set.EqOn (cuspFunction 1 f) (const ‚ÑÇ (cuspFunction 1 f 0)) (Metric.ball 0 1) := by
  refine eq_const_of_exists_le (fun q hq ‚Ü¶ ?_) (exp_nonneg (-œÄ)) ?_ (fun q hq ‚Ü¶ ?_)
  ¬∑ exact (differentiableAt_cuspFunction 1 f (mem_ball_zero_iff.mp hq)).differentiableWithinAt
  ¬∑ simp only [exp_lt_one_iff, Left.neg_neg_iff, pi_pos]
  ¬∑ simp only [Metric.mem_closedBall, dist_zero_right]
    rcases eq_or_ne q 0 with rfl | hq'
    ¬∑ refine ‚ü®0, by simpa only [norm_zero] using exp_nonneg _, le_rfl‚ü©
    ¬∑ obtain ‚ü®Œæ, hŒæ, hŒæ‚ÇÇ‚ü© := exists_one_half_le_im_and_norm_le hk f
        ‚ü®_, im_invQParam_pos_of_norm_lt_one Real.zero_lt_one (mem_ball_zero_iff.mp hq) hq'‚ü©
      exact ‚ü®_, norm_qParam_le_of_one_half_le_im hŒæ,
        by simpa only [‚Üê eq_cuspFunction 1 f, Nat.cast_one, coe_mk_subtype,
          qParam_right_inv one_ne_zero hq'] using hŒæ‚ÇÇ‚ü©

private theorem levelOne_nonpos_wt_const (hk : k ‚â§ 0) (f : F) :
    ‚áëf = Function.const _ (cuspFunction 1 f 0) := by
  ext z
  have hQ : ùï¢ 1 z ‚àà (Metric.ball 0 1) := by
    simpa only [Metric.mem_ball, dist_zero_right, neg_mul, mul_zero, div_one, Real.exp_zero]
      using (norm_qParam_lt_iff zero_lt_one 0 z.1).mpr z.2
  simpa only [‚Üê eq_cuspFunction 1 f z, Nat.cast_one, Function.const_apply] using
    (cuspFunction_eqOn_const_of_nonpos_wt hk f) hQ

lemma levelOne_neg_weight_eq_zero (hk : k < 0) (f : F) : ‚áëf = 0 := by
  have hf := levelOne_nonpos_wt_const hk.le f
  rcases wt_eq_zero_of_eq_const k hf with rfl | hf‚ÇÄ
  ¬∑ exact (lt_irrefl _ hk).elim
  ¬∑ rw [hf, hf‚ÇÄ, const_zero]

lemma levelOne_weight_zero_const [ModularFormClass F (Œì(1).map <| mapGL ‚Ñù) 0] (f : F) :
    ‚àÉ c, ‚áëf = Function.const _ c :=
  ‚ü®_, levelOne_nonpos_wt_const le_rfl f‚ü©

end ModularFormClass

lemma ModularForm.levelOne_weight_zero_rank_one :
    Module.rank ‚ÑÇ (ModularForm (Œì(1).map <| mapGL ‚Ñù) 0) = 1 := by
  refine rank_eq_one (const 1) (by simp [DFunLike.ne_iff]) fun g ‚Ü¶ ?_
  obtain ‚ü®c', hc'‚ü© := levelOne_weight_zero_const g
  aesop

lemma ModularForm.levelOne_neg_weight_rank_zero (hk : k < 0) :
    Module.rank ‚ÑÇ (ModularForm (Œì(1).map <| mapGL ‚Ñù) k) = 0 := by
  refine rank_eq_zero_iff.mpr fun f ‚Ü¶ ‚ü®_, one_ne_zero, ?_‚ü©
  simpa only [one_smul, ‚Üê DFunLike.coe_injective.eq_iff] using levelOne_neg_weight_eq_zero hk f
