/-
Copyright (c) 2024 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/
import Mathlib.Analysis.Complex.AbsMax
import Mathlib.NumberTheory.Modular
import Mathlib.NumberTheory.ModularForms.QExpansion
/-!
# Level one modular forms

This file contains results specific to modular forms of level one, ie. modular forms for `SL(2, â„¤)`.

TODO: Add finite-dimensionality of these spaces of modular forms.

-/

open UpperHalfPlane ModularGroup SlashInvariantForm ModularForm Complex
  CongruenceSubgroup Real Function SlashInvariantFormClass ModularFormClass Periodic

local notation "ğ•¢" => qParam

variable {F : Type*} [FunLike F â„ â„‚] {k : â„¤}

namespace SlashInvariantForm

variable [SlashInvariantFormClass F Î“(1) k]

lemma exists_one_half_le_im_and_norm_le (hk : k â‰¤ 0) (f : F) (Ï„ : â„) :
    âˆƒ Î¾ : â„, 1 / 2 â‰¤ Î¾.im âˆ§ â€–f Ï„â€– â‰¤ â€–f Î¾â€– :=
  let âŸ¨Î³, hÎ³, hdenomâŸ© := exists_one_half_le_im_smul_and_norm_denom_le Ï„
  âŸ¨Î³ â€¢ Ï„, hÎ³, by simpa only [slash_action_eqn'' _ (mem_Gamma_one Î³),
    norm_mul, norm_zpow] using le_mul_of_one_le_left (norm_nonneg _) <|
      one_le_zpow_of_nonposâ‚€ (norm_pos_iff.2 (denom_ne_zero _ _)) hdenom hkâŸ©

variable (k) in
/-- If a constant function is modular of weight `k`, then either `k = 0`, or the constant is `0`. -/
lemma wt_eq_zero_of_eq_const {f : F} {c : â„‚} (hf : â‡‘f = Function.const _ c) :
    k = 0 âˆ¨ c = 0 := by
  have hI := slash_action_eqn'' f (mem_Gamma_one S) I
  have h2I2 := slash_action_eqn'' f (mem_Gamma_one S) âŸ¨2 * Complex.I, by norm_numâŸ©
  simp_rw [sl_moeb, hf, Function.const, denom_S, coe_mk_subtype] at hI h2I2
  nth_rw 1 [h2I2] at hI
  simp only [mul_zpow, coe_I, mul_eq_mul_right_iff, mul_left_eq_selfâ‚€] at hI
  refine hI.imp_left (Or.casesOn Â· (fun H â†¦ ?_) (False.elim âˆ˜ zpow_ne_zero k I_ne_zero))
  rwa [â† ofReal_ofNat, â† ofReal_zpow, â† ofReal_one, ofReal_inj,
    zpow_eq_one_iff_rightâ‚€ (by norm_num) (by norm_num)] at H

end SlashInvariantForm

namespace ModularFormClass

variable [ModularFormClass F Î“(1) k]

private theorem cuspFunction_eqOn_const_of_nonpos_wt (hk : k â‰¤ 0) (f : F) :
    Set.EqOn (cuspFunction 1 f) (const â„‚ (cuspFunction 1 f 0)) (Metric.ball 0 1) := by
  refine eq_const_of_exists_le (fun q hq â†¦ ?_) (exp_nonneg (-Ï€)) ?_ (fun q hq â†¦ ?_)
  Â· exact (differentiableAt_cuspFunction 1 f (mem_ball_zero_iff.mp hq)).differentiableWithinAt
  Â· simp only [exp_lt_one_iff, Left.neg_neg_iff, pi_pos]
  Â· simp only [Metric.mem_closedBall, dist_zero_right]
    rcases eq_or_ne q 0 with rfl | hq'
    Â· refine âŸ¨0, by simpa only [norm_zero] using exp_nonneg _, le_rflâŸ©
    Â· obtain âŸ¨Î¾, hÎ¾, hÎ¾â‚‚âŸ© := exists_one_half_le_im_and_norm_le hk f
        âŸ¨_, im_invQParam_pos_of_norm_lt_one Real.zero_lt_one (mem_ball_zero_iff.mp hq) hq'âŸ©
      exact âŸ¨_, norm_qParam_le_of_one_half_le_im hÎ¾,
        by simpa only [â† eq_cuspFunction 1 f, Nat.cast_one, coe_mk_subtype,
          qParam_right_inv one_ne_zero hq'] using hÎ¾â‚‚âŸ©

private theorem levelOne_nonpos_wt_const (hk : k â‰¤ 0) (f : F) :
    â‡‘f = Function.const _ (cuspFunction 1 f 0) := by
  ext z
  have hQ : ğ•¢ 1 z âˆˆ (Metric.ball 0 1) := by
    simpa only [Metric.mem_ball, dist_zero_right, neg_mul, mul_zero, div_one, Real.exp_zero]
      using (norm_qParam_lt_iff zero_lt_one 0 z.1).mpr z.2
  simpa only [â† eq_cuspFunction 1 f z, Nat.cast_one, Function.const_apply] using
    (cuspFunction_eqOn_const_of_nonpos_wt hk f) hQ

lemma levelOne_neg_weight_eq_zero (hk : k < 0) (f : F) : â‡‘f = 0 := by
  have hf := levelOne_nonpos_wt_const hk.le f
  rcases wt_eq_zero_of_eq_const k hf with rfl | hfâ‚€
  Â· exact (lt_irrefl _ hk).elim
  Â· rw [hf, hfâ‚€, const_zero]

lemma levelOne_weight_zero_const [ModularFormClass F Î“(1) 0] (f : F) :
    âˆƒ c, â‡‘f = Function.const _ c :=
  âŸ¨_, levelOne_nonpos_wt_const le_rfl fâŸ©

end ModularFormClass

lemma ModularForm.levelOne_weight_zero_rank_one : Module.rank â„‚ (ModularForm Î“(1) 0) = 1 := by
  refine rank_eq_one (const 1) (by simp [DFunLike.ne_iff]) fun g â†¦ ?_
  obtain âŸ¨c', hc'âŸ© := levelOne_weight_zero_const g
  aesop

lemma ModularForm.levelOne_neg_weight_rank_zero (hk : k < 0) :
    Module.rank â„‚ (ModularForm Î“(1) k) = 0 := by
  refine rank_eq_zero_iff.mpr fun f â†¦ âŸ¨_, one_ne_zero, ?_âŸ©
  simpa only [one_smul, â† DFunLike.coe_injective.eq_iff] using levelOne_neg_weight_eq_zero hk f
