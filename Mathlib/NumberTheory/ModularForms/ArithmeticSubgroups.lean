/-
Copyright (c) 2025 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Data.Real.Basic
import Mathlib.GroupTheory.Commensurable
import Mathlib.LinearAlgebra.Matrix.GeneralLinearGroup.Defs
import Mathlib.Topology.Algebra.IsUniformGroup.Basic
import Mathlib.Topology.Algebra.Ring.Real
import Mathlib.Topology.Instances.Matrix
import Mathlib.Topology.MetricSpace.Isometry

/-!
# Arithmetic subgroups of `GL(2, â„)`

We define a subgroup of `GL (Fin 2) â„` to be *arithmetic* if it is commensurable with the image
of `SL(2, â„¤)`.
-/

open Matrix.SpecialLinearGroup

open scoped MatrixGroups

namespace Subgroup

/-- The image of the modular group `SL(2, â„¤)`, as a subgroup of `GL(2, â„)`. -/
scoped[MatrixGroups] notation "ğ’®â„’" => MonoidHom.range (mapGL â„ : SL(2, â„¤) â†’* GL (Fin 2) â„)

/-- Coercion from subgroups of `SL(2, â„¤)` to subgroups of `GL(2, â„)` by mapping along the obvious
inclusion homomorphism. -/
instance : Coe (Subgroup SL(2, â„¤)) (Subgroup (GL (Fin 2) â„)) where
  coe := map (mapGL â„)

/-- A subgroup of `GL(2, â„)` is arithmetic if it is commensurable with the image of `SL(2, â„¤)`. -/
class IsArithmetic (ğ’¢ : Subgroup (GL (Fin 2) â„)) : Prop where
  is_commensurable : Commensurable ğ’¢ ğ’®â„’

/-- The image of `SL(2, â„¤)` in `GL(2, â„)` is arithmetic. -/
instance : IsArithmetic ğ’®â„’ where is_commensurable := .refl ğ’®â„’

lemma isArithmetic_iff_finiteIndex {Î“ : Subgroup SL(2, â„¤)} : IsArithmetic Î“ â†” Î“.FiniteIndex := by
  constructor <;>
  Â· refine fun âŸ¨hâŸ© â†¦ âŸ¨?_âŸ©
    simpa [Commensurable, MonoidHom.range_eq_map, â† relIndex_comap,
      comap_map_eq_self_of_injective mapGL_injective] using h

/-- Images in `GL(2, â„)` of finite-index subgroups of `SL(2, â„¤)` are arithmetic. -/
instance (Î“ : Subgroup SL(2, â„¤)) [Î“.FiniteIndex] : IsArithmetic Î“ :=
  isArithmetic_iff_finiteIndex.mpr â€¹_â€º

/-- If `Î“` is arithmetic, its preimage in `SL(2, â„¤)` has finite index. -/
instance IsArithmetic.finiteIndex_comap (ğ’¢ : Subgroup (GL (Fin 2) â„)) [IsArithmetic ğ’¢] :
    (ğ’¢.comap (mapGL (R := â„¤) â„)).FiniteIndex :=
  âŸ¨ğ’¢.index_comap (mapGL (R := â„¤) â„) â–¸ IsArithmetic.is_commensurable.1âŸ©

end Subgroup

namespace Matrix.SpecialLinearGroup

lemma isInducing_toGL : Topology.IsInducing (mapGL â„ : SL(2, â„¤) â†’ GL (Fin 2) â„) := by
  refine .of_comp continuous_of_discreteTopology Units.continuous_val ?_
  refine (Topology.IsInducing.matrix_map ?_).comp âŸ¨rflâŸ©
  exact Topology.IsEmbedding.toIsInducing (Isometry.isEmbedding fun _ _ â†¦ rfl)

lemma isEmbedding_toGL : Topology.IsEmbedding (mapGL â„ : SL(2, â„¤) â†’ GL (Fin 2) â„) :=
  âŸ¨isInducing_toGL, mapGL_injectiveâŸ©

instance discreteTopology_SL2â„¤ : DiscreteTopology ğ’®â„’ :=
  isEmbedding_toGL.toHomeomorph.discreteTopology

lemma isClosed_SL2â„¤ : IsClosed (ğ’®â„’ : Set (GL (Fin 2) â„)) :=
  Subgroup.isClosed_of_discrete

lemma isClosedEmbedding_toGL : Topology.IsClosedEmbedding (mapGL â„ : SL(2, â„¤) â†’ GL (Fin 2) â„) :=
  âŸ¨isEmbedding_toGL, isClosed_SL2â„¤âŸ©

end Matrix.SpecialLinearGroup
