/-
Copyright (c) 2025 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
module

public import Mathlib.Topology.Algebra.IsUniformGroup.DiscreteSubgroup
public import Mathlib.Topology.Algebra.Ring.Real
public import Mathlib.Topology.Instances.Matrix
public import Mathlib.Topology.MetricSpace.Isometry

/-!
# Arithmetic subgroups of `GL(2, â„)`

We define a subgroup of `GL (Fin 2) â„` to be *arithmetic* if it is commensurable with the image
of `SL(2, â„¤)`.
-/

@[expose] public section

open Matrix Matrix.SpecialLinearGroup

open scoped MatrixGroups

local notation "SL" => SpecialLinearGroup

variable {n : Type*} [Fintype n] [DecidableEq n]

namespace Subgroup

section det_typeclasses

variable {R : Type*} [CommRing R] (Î“ : Subgroup (GL n R))

/-- Typeclass saying that a subgroup of `GL(2, â„)` has determinant contained in `{Â±1}`. Necessary
so that the typeclass system can detect when the slash action is multiplicative. -/
class HasDetPlusMinusOne : Prop where
  det_eq {g} (hg : g âˆˆ Î“) : g.det = 1 âˆ¨ g.det = -1

variable {Î“} in
lemma HasDetPlusMinusOne.abs_det [LinearOrder R] [IsOrderedRing R] [HasDetPlusMinusOne Î“]
    {g} (hg : g âˆˆ Î“) : |g.det.val| = 1 := by
  rcases HasDetPlusMinusOne.det_eq hg with h | h <;> simp [h]

/-- Typeclass saying that a subgroup of `GL(n, R)` is contained in `SL(n, R)`. Necessary so that
the typeclass system can detect when the slash action is `â„‚`-linear. -/
class HasDetOne : Prop where
  det_eq {g} (hg : g âˆˆ Î“) : g.det = 1

instance (Î“ : Subgroup (SL n R)) : HasDetOne (Î“.map toGL) where
  det_eq {g} hg := by rcases hg with âŸ¨g, hg, rflâŸ©; simp

instance {S : Type*} [CommRing S] [Algebra R S] (Î“ : Subgroup (SL n R)) :
    HasDetOne (Î“.map <| mapGL S) where
  det_eq {g} hg := by rcases hg with âŸ¨g, hg, rflâŸ©; simp

instance {S : Type*} [CommRing S] [Algebra R S] :
    HasDetOne (mapGL (n := n) (R := R) S).range where
  det_eq {g} hg := by rcases hg with âŸ¨g, hg, rflâŸ©; simp

instance [HasDetOne Î“] : HasDetPlusMinusOne Î“ := âŸ¨fun {g} hg â†¦ by simp [HasDetOne.det_eq hg]âŸ©

instance (Î“' : Subgroup (GL n R)) [HasDetOne Î“] : HasDetOne (Î“ âŠ“ Î“') where
  det_eq hg := HasDetOne.det_eq hg.1

instance (Î“' : Subgroup (GL n R)) [HasDetOne Î“] : HasDetOne (Î“' âŠ“ Î“) where
  det_eq hg := HasDetOne.det_eq hg.2

end det_typeclasses

section SL2Z_in_GL2R

/-- The image of the modular group `SL(2, â„¤)`, as a subgroup of `GL(2, â„)`. -/
scoped[MatrixGroups] notation "ğ’®â„’" => MonoidHom.range (mapGL â„ : SL(2, â„¤) â†’* GL (Fin 2) â„)

/-- Coercion from subgroups of `SL(2, â„¤)` to subgroups of `GL(2, â„)` by mapping along the obvious
inclusion homomorphism. -/
instance : Coe (Subgroup SL(2, â„¤)) (Subgroup (GL (Fin 2) â„)) where
  coe := map (mapGL â„)

/-- A subgroup of `GL(2, â„)` is arithmetic if it is commensurable with the image of `SL(2, â„¤)`. -/
class IsArithmetic (ğ’¢ : Subgroup (GL (Fin 2) â„)) : Prop where
  is_commensurable : Commensurable ğ’¢ ğ’®â„’

/-- The image of `SL(2, â„¤)` in `GL(2, â„)` is arithmetic. -/
instance : IsArithmetic ğ’®â„’ where is_commensurable := .refl ğ’®â„’

lemma isArithmetic_iff_finiteIndex {Î“ : Subgroup SL(2, â„¤)} : IsArithmetic Î“ â†” Î“.FiniteIndex := by
  constructor <;>
  Â· refine fun âŸ¨hâŸ© â†¦ âŸ¨?_âŸ©
    simpa [Commensurable, MonoidHom.range_eq_map, â† relIndex_comap,
      comap_map_eq_self_of_injective mapGL_injective] using h

/-- Images in `GL(2, â„)` of finite-index subgroups of `SL(2, â„¤)` are arithmetic. -/
instance (Î“ : Subgroup SL(2, â„¤)) [Î“.FiniteIndex] : IsArithmetic Î“ :=
  isArithmetic_iff_finiteIndex.mpr â€¹_â€º

/-- If `Î“` is arithmetic, its preimage in `SL(2, â„¤)` has finite index. -/
instance IsArithmetic.finiteIndex_comap (ğ’¢ : Subgroup (GL (Fin 2) â„)) [IsArithmetic ğ’¢] :
    (ğ’¢.comap (mapGL (R := â„¤) â„)).FiniteIndex :=
  âŸ¨ğ’¢.index_comap (mapGL (R := â„¤) â„) â–¸ IsArithmetic.is_commensurable.1âŸ©

instance {Î“ : Subgroup (GL (Fin 2) â„)} [h : Î“.IsArithmetic] : HasDetPlusMinusOne Î“ := by
  refine âŸ¨fun {g} hg â†¦ ?_âŸ©
  suffices |g.det.val| = 1 by rcases abs_cases g.det.val <;> aesop
  obtain âŸ¨n, hn, _, hgnâŸ© := Subgroup.exists_pow_mem_of_relIndex_ne_zero
    Subgroup.IsArithmetic.is_commensurable.2 hg
  suffices |(g.det ^ n).val| = 1 by simpa [â† abs_pow, abs_pow_eq_one _ (Nat.ne_zero_of_lt hn)]
  obtain âŸ¨t, htâŸ© := hgn.1
  have := congr_arg Matrix.GeneralLinearGroup.det ht.symm
  rw [Matrix.SpecialLinearGroup.det_mapGL, map_pow] at this
  simp [this]

instance IsArithmetic.isFiniteRelIndexSL (ğ’¢ : Subgroup (GL (Fin 2) â„)) [IsArithmetic ğ’¢] :
    ğ’¢.IsFiniteRelIndex ğ’®â„’ :=
  âŸ¨IsArithmetic.is_commensurable.1âŸ©

instance IsArithmetic.inter {Î“ Î“'} [IsArithmetic Î“] [IsArithmetic Î“'] : IsArithmetic (Î“ âŠ“ Î“') := by
  constructor
  constructor
  Â· apply relIndex_inf_ne_zero <;> exact IsArithmetic.is_commensurable.1
  Â· apply relIndex_ne_zero_trans (K := Î“) IsArithmetic.is_commensurable.2
    rw [relIndex_eq_one.mpr inf_le_left]
    simp

end SL2Z_in_GL2R

end Subgroup

namespace Matrix.SpecialLinearGroup

instance discreteSpecialLinearGroupIntRange : DiscreteTopology (mapGL (n := n) (R := â„¤) â„).range :=
  (isEmbedding_mapGL (Isometry.isEmbedding fun _ _ â†¦ rfl)).toHomeomorph.discreteTopology

lemma isClosedEmbedding_mapGLInt : Topology.IsClosedEmbedding (mapGL â„ : SL n â„¤ â†’ GL n â„) :=
  âŸ¨isEmbedding_mapGL (Isometry.isEmbedding fun _ _ â†¦ rfl), (mapGL â„).range.isClosed_of_discreteâŸ©

end Matrix.SpecialLinearGroup

/-- Arithmetic subgroups of `GL(2, â„)` are discrete. -/
instance Subgroup.IsArithmetic.discreteTopology {ğ’¢ : Subgroup (GL (Fin 2) â„)} [IsArithmetic ğ’¢] :
    DiscreteTopology ğ’¢ := by
  rw [is_commensurable.discreteTopology_iff]
  infer_instance

section adjoinNeg

variable {R : Type*} [Ring R]

/-- Given a subgroup `ğ’¢` of `GL n R`, this is the subgroup generated by `ğ’¢` and `-1`. -/
def Subgroup.adjoinNegOne (ğ’¢ : Subgroup (GL n R)) : Subgroup (GL n R) where
  carrier := {g | g âˆˆ ğ’¢ âˆ¨ -g âˆˆ ğ’¢}
  mul_mem' ha hb := by
    rcases ha with ha | ha <;>
      rcases hb with hb | hb <;>
      Â· have := mul_mem ha hb
        aesop
  one_mem' := by simp
  inv_mem' ha := by
    rcases ha with (ha | ha) <;>
    Â· have := inv_mem ha
      aesop

@[simp] lemma Subgroup.mem_adjoinNegOne_iff {ğ’¢ : Subgroup (GL n R)} {g : GL n R} :
    g âˆˆ ğ’¢.adjoinNegOne â†” g âˆˆ ğ’¢ âˆ¨ -g âˆˆ ğ’¢ :=
  Iff.rfl

lemma Subgroup.le_adjoinNegOne (ğ’¢ : Subgroup (GL n R)) : ğ’¢ â‰¤ ğ’¢.adjoinNegOne :=
  fun _ hg â†¦ .inl hg

lemma Subgroup.negOne_mem_adjoinNegOne (ğ’¢ : Subgroup (GL n R)) : -1 âˆˆ ğ’¢.adjoinNegOne :=
  by simp

@[simp] lemma Subgroup.adjoinNegOne_eq_self_iff {ğ’¢ : Subgroup (GL n R)} :
    ğ’¢.adjoinNegOne = ğ’¢ â†” -1 âˆˆ ğ’¢ :=
  âŸ¨fun h â†¦ h â–¸ negOne_mem_adjoinNegOne ğ’¢, fun hG â†¦ ğ’¢.le_adjoinNegOne.antisymm'
    fun g hg â†¦ hg.elim id (fun h â†¦ by simpa using mul_mem hG h)âŸ©

lemma Subgroup.relindex_adjoinNegOne_eq_two {ğ’¢ : Subgroup (GL n R)} (hğ’¢ : -1 âˆ‰ ğ’¢) :
    ğ’¢.relIndex ğ’¢.adjoinNegOne = 2 := by
  refine relIndex_eq_two_iff_exists_notMem_and.mpr âŸ¨_, ğ’¢.negOne_mem_adjoinNegOne, hğ’¢, ?_âŸ©
  simp [mem_adjoinNegOne_iff, or_comm]

lemma Subgroup.relIndex_adjoinNegOne_ne_zero (ğ’¢ : Subgroup (GL n R)) :
    ğ’¢.relIndex ğ’¢.adjoinNegOne â‰  0 := by
  by_cases hG : -1 âˆˆ ğ’¢
  Â· simp [adjoinNegOne_eq_self_iff.mpr hG]
  Â· simp [ğ’¢.relindex_adjoinNegOne_eq_two hG]

instance (ğ’¢ : Subgroup (GL n R)) : Subgroup.IsFiniteRelIndex ğ’¢ ğ’¢.adjoinNegOne :=
  âŸ¨ğ’¢.relIndex_adjoinNegOne_ne_zeroâŸ©

lemma Subgroup.commensurable_adjoinNegOne_self (ğ’¢ : Subgroup (GL n R)) :
    Commensurable ğ’¢.adjoinNegOne ğ’¢ :=
  âŸ¨by simp [Subgroup.relIndex_eq_one.mpr ğ’¢.le_adjoinNegOne], ğ’¢.relIndex_adjoinNegOne_ne_zeroâŸ©

instance [TopologicalSpace R] [IsTopologicalRing R] [T2Space R]
    (ğ’¢ : Subgroup (GL n R)) [DiscreteTopology ğ’¢] :
    DiscreteTopology ğ’¢.adjoinNegOne := by
  rwa [ğ’¢.commensurable_adjoinNegOne_self.discreteTopology_iff]

section CommRing

variable {R : Type*} [CommRing R]

@[simp] lemma Subgroup.hasDetPlusMinusOne_adjoinNegOne_iff {ğ’¢ : Subgroup (GL n R)} :
    ğ’¢.adjoinNegOne.HasDetPlusMinusOne â†” ğ’¢.HasDetPlusMinusOne := by
  refine âŸ¨fun _ â†¦ âŸ¨fun {g} hg â†¦ HasDetPlusMinusOne.det_eq (ğ’¢.le_adjoinNegOne hg)âŸ©, fun _ â†¦ âŸ¨?_âŸ©âŸ©
  rintro g (hg | hg)
  Â· exact HasDetPlusMinusOne.det_eq hg
  Â· by_cases hn : Even (Fintype.card n)
    Â· convert HasDetPlusMinusOne.det_eq hg using 1 <;>
        simp [Units.ext_iff, det_neg, hn]
    Â· convert (HasDetPlusMinusOne.det_eq hg).symm using 1 <;>
        simp [Units.ext_iff, det_neg, Nat.not_even_iff_odd.mp hn, neg_eq_iff_eq_neg]

lemma Subgroup.hasDetOne_adjoinNegOne_iff {ğ’¢ : Subgroup (GL n R)} (hn : Even (Fintype.card n)) :
    ğ’¢.adjoinNegOne.HasDetOne â†” ğ’¢.HasDetOne := by
  refine âŸ¨fun _ â†¦ âŸ¨fun {g} hg â†¦ HasDetOne.det_eq (ğ’¢.le_adjoinNegOne hg)âŸ©, fun _ â†¦ âŸ¨?_âŸ©âŸ©
  rintro g (hg | hg)
  Â· exact HasDetOne.det_eq hg
  Â· simpa [Units.ext_iff, det_neg, hn] using HasDetOne.det_eq hg

instance {ğ’¢ : Subgroup (GL n R)} [ğ’¢.HasDetPlusMinusOne] :
    ğ’¢.adjoinNegOne.HasDetPlusMinusOne :=
  Subgroup.hasDetPlusMinusOne_adjoinNegOne_iff.2 â€¹_â€º

instance {ğ’¢ : Subgroup (GL n R)} [ğ’¢.HasDetOne] [Fact (Even (Fintype.card n))] :
    ğ’¢.adjoinNegOne.HasDetOne :=
  (Subgroup.hasDetOne_adjoinNegOne_iff Fact.out).2 â€¹_â€º

end CommRing

instance Subgroup.instIsArithmeticAdjoinNegOne {ğ’¢ : Subgroup (GL (Fin 2) â„)} [ğ’¢.IsArithmetic] :
    ğ’¢.adjoinNegOne.IsArithmetic :=
  âŸ¨(ğ’¢.commensurable_adjoinNegOne_self).trans IsArithmetic.is_commensurableâŸ©

end adjoinNeg
