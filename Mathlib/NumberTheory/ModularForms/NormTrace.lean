/-
Copyright (c) 2025 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
module

public import Mathlib.NumberTheory.ModularForms.LevelOne

/-!
# Norm and trace maps

Given two subgroups `ùí¢, ‚Ñã` of `GL(2, ‚Ñù)` with `ùí¢.relindex ‚Ñã ‚â† 0` (i.e. `ùí¢ ‚äì ‚Ñã` has finite index
in `‚Ñã`), we define a trace map from `ModularForm (ùí¢ ‚äì ‚Ñã) k` to `ModularForm ‚Ñã k`.
-/
@[expose] public noncomputable section

open UpperHalfPlane

open scoped ModularForm Topology Filter Manifold

variable {ùí¢ ‚Ñã : Subgroup (GL (Fin 2) ‚Ñù)} {F : Type*} (f : F) [FunLike F ‚Ñç ‚ÑÇ] {k : ‚Ñ§}

local notation "ùí¨" => ‚Ñã ‚ß∏ (ùí¢.subgroupOf ‚Ñã)

instance : MulAction ‚Ñã ‚Ñã := Monoid.toMulAction ..
instance : MulAction ‚Ñã ùí¨ := .quotient ..

namespace SlashInvariantForm

variable [SlashInvariantFormClass F ùí¢ k]

/-- For `f` invariant under `ùí¢`, this is a function on `(‚Ñã ‚ß∏ ùí¢ ‚äì ‚Ñã) √ó ‚Ñç ‚Üí ‚ÑÇ` which packages up the
translates of `f` by `‚Ñã`. -/
def quotientFunc (q : ùí¨) (œÑ : ‚Ñç) : ‚ÑÇ :=
  q.liftOn (fun g ‚Ü¶ ((f : ‚Ñç ‚Üí ‚ÑÇ) ‚à£[k] g.val‚Åª¬π) œÑ) (fun h h' hhh' ‚Ü¶ by
    obtain ‚ü®j, hj, hj'‚ü© : ‚àÉ g ‚àà ùí¢, h' = h * g := by
      rw [‚Üê Quotient.eq_iff_equiv, Quotient.eq, QuotientGroup.leftRel_apply] at hhh'
      exact ‚ü®h‚Åª¬π * h', hhh', mod_cast (mul_inv_cancel_left h h').symm‚ü©
    simp [hj', SlashAction.slash_mul, SlashInvariantFormClass.slash_action_eq f j‚Åª¬π (inv_mem hj)])

lemma quotientFunc_mk (h : ‚Ñã) : quotientFunc f ‚ü¶h‚üß = (f : ‚Ñç ‚Üí ‚ÑÇ) ‚à£[k] h.val‚Åª¬π :=
  rfl

lemma quotientFunc_smul {h} (hh : h ‚àà ‚Ñã) (q : ùí¨) :
    quotientFunc f q ‚à£[k] h = quotientFunc f ((‚ü®h, hh‚ü© : ‚Ñã)‚Åª¬π ‚Ä¢ q) := by
  induction q using Quotient.inductionOn with | h r =>
  simp [quotientFunc_mk, SlashAction.slash_mul]

variable (‚Ñã)

/-- The trace of a slash-invariant form, as a slash-invariant form. -/
protected def trace [ùí¢.IsFiniteRelIndex ‚Ñã] : SlashInvariantForm ‚Ñã k where
  toFun := let := Fintype.ofFinite ùí¨; ‚àë q : ùí¨, quotientFunc f q
  slash_action_eq' h hh := by
    let := Fintype.ofFinite ùí¨
    simpa [SlashAction.sum_slash, quotientFunc_smul f hh]
      using Equiv.sum_comp (MulAction.toPerm (_ : ‚Ñã)) _

/-- The norm of a slash-invariant form, as a slash-invariant form. -/
protected def norm [ùí¢.IsFiniteRelIndex ‚Ñã] [‚Ñã.HasDetPlusMinusOne] :
    SlashInvariantForm ‚Ñã (k * Nat.card ùí¨) where
  toFun := let := Fintype.ofFinite ùí¨; ‚àè q : ùí¨, quotientFunc f q
  slash_action_eq' h hh := by
    let := Fintype.ofFinite ùí¨
    simpa [‚Üê Finset.card_univ, ModularForm.prod_slash Finset.univ_nonempty,
      quotientFunc_smul f hh, Subgroup.HasDetPlusMinusOne.abs_det hh,
      -Matrix.GeneralLinearGroup.val_det_apply] using Equiv.prod_comp (MulAction.toPerm (_ : ‚Ñã)) _

end SlashInvariantForm

open SlashInvariantForm

section ModularForm

variable (‚Ñã) [ùí¢.IsFiniteRelIndex ‚Ñã]

/-- The trace of a modular form, as a modular form. -/
protected def ModularForm.trace [ModularFormClass F ùí¢ k] : ModularForm ‚Ñã k where
  __ := SlashInvariantForm.trace ‚Ñã f
  holo' := by
    simp only [SlashInvariantForm.trace, SlashInvariantForm.coe_mk]
    refine MDifferentiable.finset_sum fun q _ ‚Ü¶ ?_
    induction q using Quotient.inductionOn with | h r =>
    simpa only [quotientFunc_mk, ‚Üê Function.comp_def (f := f ‚à£[k] r.val‚Åª¬π),
      ‚Üê UpperHalfPlane.mdifferentiable_iff] using (ModularForm.translate f r.val‚Åª¬π).holo'
  bdd_at_cusps' h Œ≥ := by
    rintro rfl
    simp_rw [SlashInvariantForm.trace, IsBoundedAtImInfty, Filter.BoundedAtFilter,
      SlashAction.sum_slash, Finset.sum_fn]
    refine .sum fun q _ ‚Ü¶ ?_
    induction q using Quotient.inductionOn with | h r =>
    obtain ‚ü®r, hr‚ü© := r
    refine (ModularForm.translate f _).bdd_at_cusps' ?_ Œ≥ rfl
    simp only [inv_inv]
    apply h.of_relindex_ne_zero
    rw [‚Üê Subgroup.conjAct_pointwise_smul_eq_self (Subgroup.le_normalizer hr),
      Subgroup.relIndex_pointwise_smul]
    exact Subgroup.IsFiniteRelIndex.relIndex_ne_zero

/-- The trace of a cusp form, as a cusp form. -/
protected def CuspForm.trace [CuspFormClass F ùí¢ k] : CuspForm ‚Ñã k where
  __ := ModularForm.trace ‚Ñã f
  zero_at_cusps' h Œ≥ := by
    rintro rfl
    simp_rw [ModularForm.trace, SlashInvariantForm.trace, IsZeroAtImInfty, Filter.ZeroAtFilter,
      SlashAction.sum_slash, Finset.sum_fn]
    let := Fintype.ofFinite ùí¨
    rw [show (0 : ‚ÑÇ) = ‚àë c : ‚Ñã ‚ß∏ ùí¢.subgroupOf ‚Ñã, 0 by simp]
    refine tendsto_finset_sum .univ fun q _ ‚Ü¶ ?_
    induction q using Quotient.inductionOn with | h r =>
    obtain ‚ü®r, hr‚ü© := r
    refine (CuspForm.translate f _).zero_at_cusps' ?_ Œ≥ rfl
    simp only [inv_inv]
    apply h.of_relindex_ne_zero
    rw [‚Üê ‚Ñã.conjAct_pointwise_smul_eq_self (‚Ñã.le_normalizer hr), ùí¢.relIndex_pointwise_smul]
    exact Subgroup.IsFiniteRelIndex.relIndex_ne_zero

/-- The norm of a modular form, as a modular form. -/
def ModularForm.norm [‚Ñã.HasDetPlusMinusOne] [ModularFormClass F ùí¢ k] :
    ModularForm ‚Ñã (k * Nat.card ùí¨) where
  __ := SlashInvariantForm.norm ‚Ñã f
  holo' := by
    simp only [SlashInvariantForm.norm, SlashInvariantForm.coe_mk]
    refine MDifferentiable.finset_prod fun q _ ‚Ü¶ ?_
    induction q using Quotient.inductionOn with | h r =>
    simpa [quotientFunc_mk, ‚Üê Function.comp_def (f := f ‚à£[k] r.val‚Åª¬π),
      ‚Üê UpperHalfPlane.mdifferentiable_iff] using (ModularForm.translate f r.val‚Åª¬π).holo'
  bdd_at_cusps' h Œ≥ := by
    rintro rfl
    let := Fintype.ofFinite ùí¨
    simp_rw [SlashInvariantForm.norm, IsBoundedAtImInfty, Filter.BoundedAtFilter]
    rw [Nat.card_eq_fintype_card, ModularForm.prod_fintype_slash]
    apply Asymptotics.IsBigO.const_smul_left
    rw [show (1 : ‚Ñç ‚Üí ‚Ñù) = (fun x ‚Ü¶ ‚àè (i : ùí¨), 1) by ext; simp, Finset.prod_fn]
    apply Asymptotics.IsBigO.finsetProd fun q _ ‚Ü¶ ?_
    induction q using Quotient.inductionOn with | h r =>
    obtain ‚ü®r, hr‚ü© := r
    refine (ModularForm.translate f _).bdd_at_cusps' ?_ Œ≥ rfl
    simp only [inv_inv]
    apply h.of_relindex_ne_zero
    rw [‚Üê Subgroup.conjAct_pointwise_smul_eq_self (Subgroup.le_normalizer hr),
      Subgroup.relIndex_pointwise_smul]
    exact Subgroup.IsFiniteRelIndex.relIndex_ne_zero

lemma ModularForm.norm_ne_zero [‚Ñã.HasDetPlusMinusOne] [ModularFormClass F ùí¢ k]
    (hf : (f : ‚Ñç ‚Üí ‚ÑÇ) ‚â† 0) (œÑ : ‚Ñç) :
    ‚àÄ·∂† z in ùìù[‚â†] œÑ, ModularForm.norm ‚Ñã f z ‚â† 0 := by
  have (q : ùí¨) : ‚àÄ·∂† z in ùìù[‚â†] œÑ, quotientFunc f q z ‚â† 0 := by
    induction q using Quotient.inductionOn with | h r =>
    simp only [quotientFunc_mk]
    contrapose! hf
    have := UpperHalfPlane.eq_zero_of_frequently
      (ModularForm.translate f r.val‚Åª¬π).holo' (œÑ := œÑ) hf
    have : (f : ‚Ñç ‚Üí ‚ÑÇ) ‚à£[k] r.val‚Åª¬π = 0 := this
    apply_fun (fun g ‚Ü¶ g ‚à£[k] r.val) at this
    rwa [‚Üê SlashAction.slash_mul, inv_mul_cancel, SlashAction.slash_one,
      SlashAction.zero_slash] at this
  filter_upwards [Filter.eventually_all.mpr this] with z hz
  simp only [ModularForm.norm, SlashInvariantForm.norm, Finset.prod_fn, ‚Üê ModularForm.toFun_eq_coe]
  exact Finset.prod_ne_zero_iff.mpr fun q _ ‚Ü¶ hz q

open scoped MatrixGroups

lemma ModularForm.isZero_of_neg_weight {Œì : Subgroup (GL (Fin 2) ‚Ñù)} [Œì.IsArithmetic]
    {k : ‚Ñ§} (hk : k < 0) (f : ModularForm Œì k) : f = 0 := by
  have : Œì.IsFiniteRelIndex ùíÆ‚Ñí := by
    constructor
    rw [MonoidHom.range_eq_map, ‚Üê Subgroup.relIndex_comap, Subgroup.relIndex_top_right]
    exact (Subgroup.IsArithmetic.finiteIndex_comap Œì).index_ne_zero
  have : ModularForm.norm ùíÆ‚Ñí f = 0 := by
    ext
    rw [@ModularFormClass.levelOne_neg_weight_eq_zero (f := ModularForm.norm ùíÆ‚Ñí f) _ _ _]
    ¬∑ tauto
    ¬∑ rw [CongruenceSubgroup.Gamma_one_top, MonoidHom.range_eq_map]
      infer_instance
    ¬∑ refine mul_neg_of_neg_of_pos hk ?_
      norm_cast
      rw [‚Üê Subgroup.index_eq_card, ‚Üê Subgroup.relIndex, ‚Üê MonoidHom.range_eq_map]
      exact Nat.pos_of_ne_zero this.relIndex_ne_zero
  by_contra hfne
  obtain ‚ü®œÑ, hœÑ‚ü© := (norm_ne_zero ùíÆ‚Ñí f (by contrapose! hfne; ext œÑ; simp [hfne]) I).exists
  simp_all



end ModularForm

end
