/-
Copyright (c) 2025 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Algebra.EuclideanDomain.Int
import Mathlib.Analysis.RCLike.Basic
import Mathlib.RingTheory.Localization.NumDen
import Mathlib.Topology.Compactification.OnePoint.ProjectiveLine
import Mathlib.NumberTheory.ModularForms.ArithmeticSubgroups

/-!
# Cusps

We define the cusps of a subgroup of `GL(2, â„)` as the fixed points of parabolic elements.
-/

open Matrix SpecialLinearGroup GeneralLinearGroup Filter Polynomial OnePoint

open scoped MatrixGroups LinearAlgebra.Projectivization

namespace OnePoint

variable {K : Type*} [Field K] [DecidableEq K]

/-- The modular group `SL(2, A)` acts transitively on `OnePoint K`, if `A` is a PID whose fraction
field is `K`. (This includes the case `A = â„¤`, `K = â„š`.) -/
lemma exists_mem_SL2 (A : Type*) [CommRing A] [IsDomain A] [Algebra A K] [IsFractionRing A K]
    [IsPrincipalIdealRing A] (c : OnePoint K) :
    âˆƒ g : SL(2, A), (mapGL K g) â€¢ âˆ = c := by
  cases c with
  | infty => exact âŸ¨1, by simpâŸ©
  | coe q =>
    obtain âŸ¨g, hg0, hg1âŸ© := (IsFractionRing.num_den_reduced A q).isCoprime.exists_SL2_col 0
    exact âŸ¨g, by simp [hg0, hg1, smul_infty_eq_ite]âŸ©

end OnePoint

section IsCusp

/-- The *cusps* of a subgroup of `GL(2, â„)` are the fixed points of parabolic elements of `g`. -/
def IsCusp (c : OnePoint â„) (ğ’¢ : Subgroup (GL (Fin 2) â„)) : Prop :=
  âˆƒ g âˆˆ ğ’¢, g.IsParabolic âˆ§ g â€¢ c = c

open Pointwise in
lemma IsCusp.smul {c : OnePoint â„} {ğ’¢ : Subgroup (GL (Fin 2) â„)} (hc : IsCusp c ğ’¢)
    (g : GL (Fin 2) â„) : IsCusp (g â€¢ c) (ConjAct.toConjAct g â€¢ ğ’¢) := by
  obtain âŸ¨p, hpğ’¢, hpp, hpcâŸ© := hc
  refine âŸ¨_, ğ’¢.smul_mem_pointwise_smul _ _ hpğ’¢, ?_, ?_âŸ©
  Â· simpa [ConjAct.toConjAct_smul] using hpp
  Â· simp [ConjAct.toConjAct_smul, MulAction.mul_smul, hpc]

lemma IsCusp.smul_of_mem {c : OnePoint â„} {ğ’¢ : Subgroup (GL (Fin 2) â„)} (hc : IsCusp c ğ’¢)
    {g : GL (Fin 2) â„} (hg : g âˆˆ ğ’¢) : IsCusp (g â€¢ c) ğ’¢ := by
  convert hc.smul g
  ext x
  rw [Subgroup.mem_pointwise_smul_iff_inv_smul_mem, â† ConjAct.toConjAct_inv,
    ConjAct.toConjAct_smul, inv_inv, Subgroup.mul_mem_cancel_right _ hg,
    Subgroup.mul_mem_cancel_left _ (inv_mem hg)]

lemma isCusp_iff_of_relIndex_ne_zero {ğ’¢ ğ’¢' : Subgroup (GL (Fin 2) â„)}
    (hğ’¢ : ğ’¢' â‰¤ ğ’¢) (hğ’¢' : ğ’¢'.relIndex ğ’¢ â‰  0) (c : OnePoint â„) :
    IsCusp c ğ’¢' â†” IsCusp c ğ’¢ := by
  refine âŸ¨fun âŸ¨g, hg, hgp, hgcâŸ© â†¦ âŸ¨g, hğ’¢ hg, hgp, hgcâŸ©, fun âŸ¨g, hg, hgp, hgcâŸ© â†¦ ?_âŸ©
  obtain âŸ¨n, hn, -, hgnâŸ© := Subgroup.exists_pow_mem_of_relIndex_ne_zero hğ’¢' hg
  refine âŸ¨g ^ n, (Subgroup.mem_inf.mpr hgn).1, hgp.pow hn.ne', ?_âŸ©
  rw [Nat.pos_iff_ne_zero] at hn
  rwa [(hgp.pow hn).smul_eq_self_iff, hgp.parabolicFixedPoint_pow hn, â† hgp.smul_eq_self_iff]

@[deprecated (since := "2025-09-13")]
alias isCusp_iff_of_relindex_ne_zero := isCusp_iff_of_relIndex_ne_zero

lemma Subgroup.Commensurable.isCusp_iff {ğ’¢ ğ’¢' : Subgroup (GL (Fin 2) â„)}
    (hğ’¢ : Commensurable ğ’¢ ğ’¢') {c : OnePoint â„} :
    IsCusp c ğ’¢ â†” IsCusp c ğ’¢' := by
  rw [â† isCusp_iff_of_relIndex_ne_zero inf_le_left, isCusp_iff_of_relIndex_ne_zero inf_le_right]
  Â· simpa [Subgroup.inf_relIndex_right] using hğ’¢.1
  Â· simpa [Subgroup.inf_relIndex_left] using hğ’¢.2

@[deprecated (since := "2025-09-17")]
alias Commensurable.isCusp_iff := Subgroup.Commensurable.isCusp_iff

/-- The cusps of `SL(2, â„¤)` are precisely the elements of `â„™Â¹(â„š)`. -/
lemma isCusp_SL2Z_iff {c : OnePoint â„} : IsCusp c ğ’®â„’ â†” c âˆˆ Set.range (OnePoint.map Rat.cast) := by
  constructor
  Â· rintro âŸ¨-, âŸ¨g, rflâŸ©, hgp, hgcâŸ©
    simpa only [hgp.smul_eq_self_iff.mp hgc] using âŸ¨(mapGL â„š g).parabolicFixedPoint,
      by simp [GeneralLinearGroup.parabolicFixedPoint, apply_ite]âŸ©
  Â· rintro âŸ¨c, rflâŸ©
    obtain âŸ¨a, rflâŸ© := c.exists_mem_SL2 â„¤
    refine âŸ¨_, âŸ¨a * ModularGroup.T * aâ»Â¹, rflâŸ©, ?_, ?_âŸ©
    Â· suffices (mapGL â„ ModularGroup.T).IsParabolic by simpa
      refine âŸ¨fun âŸ¨a, haâŸ© â†¦ zero_ne_one' â„ (by simpa [ModularGroup.T] using congr_funâ‚‚ ha 0 1), ?_âŸ©
      simp [disc_fin_two, trace_fin_two, det_fin_two, ModularGroup.T]
      norm_num
    Â· rw [â† Rat.coe_castHom, â† (Rat.castHom â„).algebraMap_toAlgebra]
      simp [OnePoint.map_smul, MulAction.mul_smul, smul_infty_eq_self_iff, ModularGroup.T]

/-- The cusps of `SL(2, â„¤)` are precisely the `SL(2, â„¤)` orbit of `âˆ`. -/
lemma isCusp_SL2Z_iff' {c : OnePoint â„} : IsCusp c ğ’®â„’ â†” âˆƒ g : SL(2, â„¤), c = mapGL â„ g â€¢ âˆ := by
  rw [isCusp_SL2Z_iff]
  constructor
  Â· rintro âŸ¨c, rflâŸ©
    obtain âŸ¨g, rflâŸ© := c.exists_mem_SL2 â„¤
    refine âŸ¨g, ?_âŸ©
    rw [â† Rat.coe_castHom, OnePoint.map_smul, OnePoint.map_infty,
      â† (Rat.castHom â„).algebraMap_toAlgebra, g.map_mapGL]
  Â· rintro âŸ¨g, rflâŸ©
    refine âŸ¨mapGL â„š g â€¢ âˆ, ?_âŸ©
    rw [â† Rat.coe_castHom, OnePoint.map_smul, OnePoint.map_infty,
       â† (Rat.castHom â„).algebraMap_toAlgebra, g.map_mapGL]

/-- The cusps of any arithmetic subgroup are the same as those of `SL(2, â„¤)`. -/
lemma Subgroup.IsArithmetic.isCusp_iff_isCusp_SL2Z (ğ’¢ : Subgroup (GL (Fin 2) â„)) [ğ’¢.IsArithmetic]
    {c : OnePoint â„} : IsCusp c ğ’¢ â†” IsCusp c ğ’®â„’ :=
  is_commensurable.isCusp_iff

end IsCusp

section CuspOrbits
/-!
## Cusp orbits

We consider the orbits for the action of `ğ’¢` on its own cusps. The main result is that if
`[ğ’¢.IsArithmetic]` holds, then this set is finite.
-/

/-- The action of `ğ’¢` on its own cusps. -/
def cusps_subMulAction (ğ’¢ : Subgroup (GL (Fin 2) â„)) : SubMulAction ğ’¢ (OnePoint â„) where
  carrier := {c | IsCusp c ğ’¢}
  smul_mem' g _ hc := IsCusp.smul_of_mem hc g.property

/-- The type of cusp orbits of `ğ’¢`, i.e. orbits for the action of `ğ’¢` on its own cusps. -/
abbrev CuspOrbits (ğ’¢ : Subgroup (GL (Fin 2) â„)) :=
  MulAction.orbitRel.Quotient ğ’¢ (cusps_subMulAction ğ’¢)

/-- Surjection from `SL(2, â„¤) / (ğ’¢ âŠ“ SL(2, â„¤))` to cusp orbits of `ğ’¢`. Mostly useful for showing
that `CuspOrbits ğ’¢` is finite for arithmetic subgroups. -/
noncomputable def cosetToCuspOrbit (ğ’¢ : Subgroup (GL (Fin 2) â„)) [ğ’¢.IsArithmetic] :
    SL(2, â„¤) â§¸ (ğ’¢.comap <| mapGL â„) â†’ CuspOrbits ğ’¢ :=
  Quotient.lift
    (fun g â†¦ âŸ¦âŸ¨mapGL â„ gâ»Â¹ â€¢ âˆ,
      (Subgroup.IsArithmetic.isCusp_iff_isCusp_SL2Z ğ’¢).mpr <| isCusp_SL2Z_iff.mpr
        âŸ¨mapGL â„š gâ»Â¹ â€¢ âˆ, by rw [â† Rat.coe_castHom, OnePoint.map_smul, OnePoint.map_infty,
          â† (Rat.castHom â„).algebraMap_toAlgebra, map_mapGL]âŸ©âŸ©âŸ§)
    (fun a b hab â†¦ by
      rw [â† Quotient.eq_iff_equiv, Quotient.eq, QuotientGroup.leftRel_apply] at hab
      refine Quotient.eq.mpr âŸ¨âŸ¨_, habâŸ©, ?_âŸ©
      simp [MulAction.mul_smul])

@[simp]
lemma cosetToCuspOrbit_apply_mk {ğ’¢ : Subgroup (GL (Fin 2) â„)} [ğ’¢.IsArithmetic] (g : SL(2, â„¤)) :
    cosetToCuspOrbit ğ’¢ âŸ¦gâŸ§ = âŸ¦âŸ¨mapGL â„ gâ»Â¹ â€¢ âˆ,
    (Subgroup.IsArithmetic.isCusp_iff_isCusp_SL2Z ğ’¢).mpr <| isCusp_SL2Z_iff.mpr
      âŸ¨mapGL â„š gâ»Â¹ â€¢ âˆ, by rw [â† Rat.coe_castHom, OnePoint.map_smul, OnePoint.map_infty,
        â† (Rat.castHom â„).algebraMap_toAlgebra, map_mapGL]âŸ©âŸ©âŸ§ :=
  rfl

lemma surjective_cosetToCuspOrbit (ğ’¢ : Subgroup (GL (Fin 2) â„)) [ğ’¢.IsArithmetic] :
    (cosetToCuspOrbit ğ’¢).Surjective := by
  rintro âŸ¨c, (hc : IsCusp c _)âŸ©
  rw [Subgroup.IsArithmetic.isCusp_iff_isCusp_SL2Z, isCusp_SL2Z_iff'] at hc
  obtain âŸ¨g, rflâŸ© := hc
  use âŸ¦gâ»Â¹âŸ§
  aesop

/-- An arithmetic subgroup has finitely many cusp orbits. -/
instance (ğ’¢ : Subgroup (GL (Fin 2) â„)) [ğ’¢.IsArithmetic] : Finite (CuspOrbits ğ’¢) :=
  .of_surjective _ (surjective_cosetToCuspOrbit ğ’¢)

end CuspOrbits

section Width
/-!
## Width of a cusp

We define the *strict width* of `ğ’¢` at `âˆ` to be the smallest `h > 0` such that `[1, h; 0, 1] âˆˆ ğ’¢`,
or `0` if no such `h` exists. (We say "strict" because the width of a subgroup `Î“ âŠ† SL(2, â„¤)` is
usually defined to be the smallest `h > 0` such that `Â±[1, h; 0, 1] âˆˆ Î“`, but we assume the
positive sign here.)
-/

variable (K : Type*) [Ring K]

/-- The map sending `x` to `[1, x; 0, 1]` (bundled as an `AddChar`). -/
def Matrix.GeneralLinearGroup.upperRightHom : AddChar K (GL (Fin 2) K) where
  toFun x := âŸ¨!![1, x; 0, 1], !![1, -x; 0, 1], by simp [one_fin_two], by simp [one_fin_two]âŸ©
  map_zero_eq_one' := by simp [Units.ext_iff, one_fin_two]
  map_add_eq_mul' a b := by simp [Units.ext_iff, add_comm]

variable {K} in
@[simp] lemma Matrix.GeneralLinearGroup.upperRightHom_apply {x : K} : (upperRightHom K x) =
    âŸ¨!![1, x; 0, 1], !![1, -x; 0, 1], by simp [one_fin_two], by simp [one_fin_two]âŸ© :=
  rfl

lemma continuous_upperRightHom [TopologicalSpace K] [IsTopologicalRing K] :
    Continuous (upperRightHom K) := by
  simp only [continuous_induced_rng, Function.comp_def, upperRightHom_apply,
    Units.embedProduct_apply, Units.inv_mk, continuous_prodMk, MulOpposite.unop_op]
  constructor <;>
  Â· refine continuous_matrix fun i j â†¦ ?_
    fin_cases i <;>
    fin_cases j <;>
    simp [continuous_const, continuous_neg, continuous_id']

lemma injective_upperRightHom : Function.Injective (upperRightHom K) := by
  refine (injective_iff_map_eq_zero (upperRightHom K).toAddMonoidHom).mpr ?_
  simp [Units.ext_iff, one_fin_two]

variable {K}

namespace Subgroup.HasDetPlusMinusOne

variable {ğ’¢ : Subgroup (GL (Fin 2) â„)} [ğ’¢.HasDetPlusMinusOne]

lemma isParabolic_iff_of_upperTriangular {g} (hg : g âˆˆ ğ’¢) (hg10 : g 1 0 = 0) :
    g.IsParabolic â†” (âˆƒ x â‰  0, g = upperRightHom â„ x) âˆ¨ (âˆƒ x â‰  0, g = -upperRightHom â„ x) := by
  rw [GeneralLinearGroup.isParabolic_iff_of_upperTriangular hg10]
  constructor
  Â· rintro âŸ¨hg00, hg01âŸ©
    have : g 1 1 ^ 2 = 1 := by
      have : g.det = g 1 1 ^ 2 := by rw [val_det_apply, det_fin_two, hg10, hg00]; ring
      have h_det : g.det = 1 âˆ¨ g.det = -1 := HasDetPlusMinusOne.det_eq hg
      simp only [Units.ext_iff, Units.val_one, Units.val_neg, this] at h_det
      exact h_det.resolve_right (neg_one_lt_zero.trans_le <| sq_nonneg _).ne'
    apply (sq_eq_one_iff.mp this).imp <;> intro hg11 <;> simp only [Units.ext_iff]
    Â· refine âŸ¨g 0 1, hg01, ?_âŸ©
      rw [g.val.eta_fin_two]
      simp [upperRightHom_apply, hg00, hg10, hg11]
    Â· refine âŸ¨-g 0 1, neg_eq_zero.not.mpr hg01, ?_âŸ©
      rw [g.val.eta_fin_two]
      simp [upperRightHom_apply, hg00, hg10, hg11]
  Â· rintro (âŸ¨x, hx, rflâŸ© | âŸ¨x, hx, rflâŸ©) <;>
    simpa using hx

end Subgroup.HasDetPlusMinusOne

/-- For a subgroup `ğ’¢` of `GL(2, K)`, this is the additive group of `x : K` such that
`[1, x; 0, 1] âˆˆ ğ’¢`. -/
def Subgroup.strictPeriods (ğ’¢ : Subgroup (GL (Fin 2) K)) : AddSubgroup K :=
  (toAddSubgroup ğ’¢).comap (upperRightHom K).toAddMonoidHom

/-- For a subgroup `ğ’¢` of `GL(2, K)`, this is the additive group of `x : K` such that
`Â±[1, x; 0, 1] âˆˆ ğ’¢`. -/
noncomputable def Subgroup.periods (ğ’¢ : Subgroup (GL (Fin 2) K)) : AddSubgroup K :=
  ğ’¢.adjoinNegOne.strictPeriods

@[simp] lemma Subgroup.mem_strictPeriods_iff {ğ’¢ : Subgroup (GL (Fin 2) K)} {x : K} :
    x âˆˆ ğ’¢.strictPeriods â†” upperRightHom K x âˆˆ ğ’¢ := by
  simp [strictPeriods]

@[simp] lemma Subgroup.strictPeriods_SL2Z : strictPeriods ğ’®â„’ = AddSubgroup.zmultiples 1 := by
  ext x
  simp only [mem_strictPeriods_iff, MonoidHom.mem_range, Units.ext_iff, mapGL_coe_matrix,
    map_apply_coe]
  refine âŸ¨fun âŸ¨g, hgâŸ© â†¦ âŸ¨g 0 1, by simpa using congr_funâ‚‚ hg 0 1âŸ©, ?_âŸ©
  rintro âŸ¨m, rflâŸ©
  use ModularGroup.T ^ m
  ext i j
  fin_cases i <;> fin_cases j <;> simp [ModularGroup.coe_T_zpow]

/-- If `ğ’¢` is discrete, so is its strict period subgroup. -/
instance [TopologicalSpace K] [IsTopologicalRing K] (ğ’¢ : Subgroup (GL (Fin 2) K))
    [hG : DiscreteTopology ğ’¢] : DiscreteTopology ğ’¢.strictPeriods := by
  let H := â†‘ğ’¢ âˆ© (Set.range (upperRightHom K))
  have hH : DiscreteTopology H := hG.of_subset Set.inter_subset_left
  have : Set.MapsTo (upperRightHom K) ğ’¢.strictPeriods H := fun x hx â†¦ by
    rw [SetLike.mem_coe, Subgroup.mem_strictPeriods_iff] at hx
    tauto
  exact .of_continuous_injective ((continuous_upperRightHom K).restrict this)
    (this.restrict_inj.mpr (injective_upperRightHom K).injOn)

lemma AddSubgroup.discrete_iff_cyclic {A : AddSubgroup â„} :
    IsAddCyclic A â†” DiscreteTopology A := by
  rw [AddSubgroup.isAddCyclic_iff_exists_zmultiples_eq_top]
  constructor
  Â· rintro âŸ¨g, rflâŸ©
    apply NormedSpace.discreteTopology_zmultiples
  Â· intro hA
    have := A.dense_or_cyclic
    simp only [â† AddSubgroup.zmultiples_eq_closure, Eq.comm (a := A)] at this
    refine this.resolve_left fun h â†¦ ?_
    -- remains to show a contradiction assuming `A` is dense and discrete
    obtain âŸ¨U, hUâŸ© := discreteTopology_subtype_iff'.mp hA 0 (by simp)
    obtain âŸ¨j, hjâŸ© := mem_closure_iff.mp (h.diff_singleton 0 0) U hU.1
      (by simpa only [â† Set.singleton_subset_iff, â† hU.2] using Set.inter_subset_left)
    grind

/-- The strict width of the cusp `âˆ`, i.e. the `x` such that `ğ’¢.strictPeriods = zmultiples x`, or
0 if no such `x` exists. -/
noncomputable def Subgroup.strictWidthInfty (ğ’¢ : Subgroup (GL (Fin 2) â„)) : â„ :=
  by classical exact if h : DiscreteTopology ğ’¢.strictPeriods then
  |Exists.choose <| ğ’¢.strictPeriods.isAddCyclic_iff_exists_zmultiples_eq_top.mp
      <| AddSubgroup.discrete_iff_cyclic.mpr h|
  else 0

/-- The width of the cusp `âˆ`, i.e. the `x` such that `ğ’¢.periods = zmultiples x`, or 0 if no such
`x` exists. -/
noncomputable def Subgroup.widthInfty (ğ’¢ : Subgroup (GL (Fin 2) â„)) : â„ :=
  ğ’¢.adjoinNegOne.strictWidthInfty

lemma Subgroup.strictWidth_nonneg (ğ’¢ : Subgroup (GL (Fin 2) â„)) : 0 â‰¤ ğ’¢.strictWidthInfty := by
  unfold Subgroup.strictWidthInfty; aesop

lemma Subgroup.strictPeriods_eq_zmultiples_strictWidth {ğ’¢ : Subgroup (GL (Fin 2) â„)}
    [DiscreteTopology ğ’¢.strictPeriods] :
    ğ’¢.strictPeriods = AddSubgroup.zmultiples ğ’¢.strictWidthInfty := by
  simp only [Subgroup.strictWidthInfty, dif_pos]
  -- the following should be a simp lemma `AddSubgroup.zmultiples_abs`
  have (a : â„) : AddSubgroup.zmultiples |a| = AddSubgroup.zmultiples a := by
    rcases abs_cases a with (h | h) <;> simp only [h, AddSubgroup.zmultiples_neg]
  rw [this, Exists.choose_spec <| ğ’¢.strictPeriods.isAddCyclic_iff_exists_zmultiples_eq_top.mp
    <| AddSubgroup.discrete_iff_cyclic.mpr inferInstance]

lemma Subgroup.strictWidthInfty_mem_strictPeriods (ğ’¢ : Subgroup (GL (Fin 2) â„)) :
    ğ’¢.strictWidthInfty âˆˆ ğ’¢.strictPeriods := by
  by_cases h : DiscreteTopology ğ’¢.strictPeriods
  Â· simp [strictPeriods_eq_zmultiples_strictWidth]
  Â· simpa only [strictWidthInfty, dif_neg h] using ğ’¢.strictPeriods.zero_mem

lemma Subgroup.strictWidth_pos_iff {ğ’¢ : Subgroup (GL (Fin 2) â„)} [DiscreteTopology ğ’¢]
    [ğ’¢.HasDetPlusMinusOne] : 0 < ğ’¢.strictWidthInfty â†” IsCusp âˆ ğ’¢ := by
  constructor
  Â· refine fun h â†¦ âŸ¨_, mem_strictPeriods_iff.mpr ğ’¢.strictWidthInfty_mem_strictPeriods, ?_, ?_âŸ©
    Â· rw [GeneralLinearGroup.isParabolic_iff_of_upperTriangular (by simp)]
      simpa using h.ne'
    Â· rw [smul_infty_eq_self_iff]
      simp
  Â· -- Hard implication: if `âˆ` is a cusp, show the strict width is positive.
    rintro âŸ¨g, hgg, hgp, hgiâŸ©
    apply ğ’¢.strictWidth_nonneg.lt_of_ne'
    rw [â† AddSubgroup.zmultiples_ne_bot]
    simp only [AddSubgroup.ne_bot_iff_exists_ne_zero, Subtype.exists, Ne, AddSubgroup.mk_eq_zero,
      exists_prop, and_comm, â† strictPeriods_eq_zmultiples_strictWidth, mem_strictPeriods_iff]
    -- We have some `g âˆˆ ğ’¢` which is parabolic and fixes `âˆ`. So `g = Â±[1, x; 0, 1]` some `x â‰  0`.
    rw [smul_infty_eq_self_iff] at hgi
    rw [Subgroup.HasDetPlusMinusOne.isParabolic_iff_of_upperTriangular hgg hgi] at hgp
    rcases hgp with âŸ¨x, hx, rflâŸ© | âŸ¨x, hx, rflâŸ©
    Â· -- If `g = [1, x; 0, 1]`, we're done
      exact âŸ¨x, hx, hggâŸ©
    Â· -- If `g = -[1, x; 0, 1]` then `g ^ 2 = [1, 2 * x; 0, 1]`.
      exact âŸ¨2 â€¢ x, by grind,
        by simpa only [AddChar.map_nsmul_eq_pow, neg_sq] using pow_mem hgg 2âŸ©

end Width
