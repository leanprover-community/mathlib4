/-
Copyright (c) 2024 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck, David Loeffler
-/

import Mathlib.Analysis.Complex.LocallyUniformLimit
import Mathlib.Analysis.Complex.UpperHalfPlane.Exp
import Mathlib.Analysis.NormedSpace.MultipliableUniformlyOn

/-!
# Dedekind eta function

## Main definitions

* We define the Dedekind eta function as the infinite product
`Œ∑(z) = q ^ 1/24 * ‚àè' (1 - q ^ (n + 1))` where `q = e ^ (2œÄiz)` and `z` is in the upper half-plane.
The product is taken over all non-negative integers `n`. We then show it is non-vanishing and
differentiable on the upper half-plane.

## References
* [F. Diamond and J. Shurman, *A First Course in Modular Forms*][diamondshurman2005], section 1.2
-/

open TopologicalSpace Set MeasureTheory intervalIntegral
 Metric Filter Function Complex

open UpperHalfPlane hiding I

open scoped Interval Real NNReal ENNReal Topology BigOperators Nat

local notation "ùï¢" => Periodic.qParam

local notation "‚Ñç‚Çí" => upperHalfPlaneSet

namespace ModularForm

/-- The q term inside the product defining the eta function. It is defined as
`eta_q n z = e ^ (2 œÄ i (n + 1) z)`. -/
noncomputable abbrev eta_q (n : ‚Ñï) (z : ‚ÑÇ) := (ùï¢ 1 z) ^ (n + 1)

lemma eta_q_eq_cexp (n : ‚Ñï) (z : ‚ÑÇ) : eta_q n z = cexp (2 * œÄ * I * (n + 1) * z) := by
  simp [eta_q, Periodic.qParam, ‚Üê Complex.exp_nsmul]
  ring_nf

lemma eta_q_eq_pow (n : ‚Ñï) (z : ‚ÑÇ) : eta_q n z = cexp (2 * œÄ * I * z) ^ (n + 1) := by
  simp [eta_q, Periodic.qParam]

lemma one_sub_eta_q_ne_zero (n : ‚Ñï) {z : ‚ÑÇ} (hz : z ‚àà ‚Ñç‚Çí) : 1 - eta_q n z ‚â† 0 := by
  rw [eta_q_eq_cexp, sub_ne_zero]
  intro h
  simpa [‚Üê mul_assoc, ‚Üê h] using norm_exp_two_pi_I_lt_one ‚ü®(n + 1) ‚Ä¢ z, by
    simpa [(show 0 < (n + 1 : ‚Ñù) by positivity)] using hz‚ü©

/-- The eta function, whose value at z is `q^ 1 / 24 * ‚àè' 1 - q ^ (n + 1)` for `q = e ^ 2 œÄ i z`. -/
noncomputable def eta (z : ‚ÑÇ) := ùï¢ 24 z * ‚àè' n, (1 - eta_q n z)

local notation "Œ∑" => eta

theorem summable_eta_q (z : ‚Ñç) : Summable fun n ‚Ü¶ ‚Äñ-eta_q n z‚Äñ := by
  simp [eta_q, eta_q_eq_pow, summable_nat_add_iff 1, norm_exp_two_pi_I_lt_one z]

lemma multipliableLocallyUniformlyOn_eta :
    MultipliableLocallyUniformlyOn (fun n a ‚Ü¶ 1 - eta_q n a) ‚Ñç‚Çí:= by
  use fun z ‚Ü¶ ‚àè' n, (1 - eta_q n z)
  simp_rw [sub_eq_add_neg]
  apply hasProdLocallyUniformlyOn_of_forall_compact isOpen_upperHalfPlaneSet
  intro K hK hcK
  by_cases hN : K.Nonempty
  ¬∑ have hc : ContinuousOn (fun x ‚Ü¶ ‚Äñcexp (2 * œÄ * I * x)‚Äñ) K := by fun_prop
    obtain ‚ü®z, hz, hB, HB‚ü© := hcK.exists_sSup_image_eq_and_ge hN hc
    apply (summable_eta_q ‚ü®z, hK hz‚ü©).hasProdUniformlyOn_nat_one_add hcK
    ¬∑ filter_upwards with n x hx
      simpa [eta_q, eta_q_eq_pow] using pow_le_pow_left‚ÇÄ (by simp [norm_nonneg]) (HB x hx) _
    ¬∑ simp_rw [eta_q, Periodic.qParam]
      fun_prop
  ¬∑ rw [hasProdUniformlyOn_iff_tendstoUniformlyOn]
    simpa [not_nonempty_iff_eq_empty.mp hN] using tendstoUniformlyOn_empty

/-- Eta is non-vanishing on the upper half plane. -/
lemma eta_ne_zero {z : ‚ÑÇ} (hz : z ‚àà ‚Ñç‚Çí) : Œ∑ z ‚â† 0 := by
  apply mul_ne_zero (Periodic.qParam_ne_zero z)
  refine tprod_one_add_ne_zero_of_summable (f := fun n ‚Ü¶ -eta_q n z)  ?_ ?_
  ¬∑ exact fun i ‚Ü¶ by simpa using one_sub_eta_q_ne_zero i hz
  ¬∑ simpa [eta_q, ‚Üê summable_norm_iff] using summable_eta_q ‚ü®z, hz‚ü©

lemma logDeriv_one_sub_cexp (r : ‚ÑÇ) : logDeriv (fun z ‚Ü¶ 1 - r * cexp z) =
    fun z ‚Ü¶ -r * cexp z / (1 - r * cexp z) := by
  ext z
  simp [logDeriv]

lemma logDeriv_one_sub_mul_cexp_comp (r : ‚ÑÇ) {g : ‚ÑÇ ‚Üí ‚ÑÇ} (hg : Differentiable ‚ÑÇ g) :
    logDeriv ((fun z ‚Ü¶ 1 - r * cexp z) ‚àò g) =
    fun z ‚Ü¶ -r * (deriv g z) * cexp (g z) / (1 - r * cexp (g z)) := by
  ext y
  rw [logDeriv_comp (by fun_prop) (hg y), logDeriv_one_sub_cexp]
  ring

private theorem one_sub_eta_logDeriv_eq (z : ‚ÑÇ) (n : ‚Ñï) :
    logDeriv (1 - eta_q n ¬∑) z = 2 * œÄ * I * (n + 1) * -eta_q n z / (1 - eta_q n z) := by
  have h2 : (fun x ‚Ü¶ 1 - cexp (2 * ‚ÜëœÄ * I * (n + 1) * x)) =
      ((fun z ‚Ü¶ 1 - 1 * cexp z) ‚àò fun x ‚Ü¶ 2 * ‚ÜëœÄ * I * (n + 1) * x) := by aesop
  have h3 : deriv (fun x : ‚ÑÇ ‚Ü¶ (2 * œÄ * I * (n + 1) * x)) =
      fun _ ‚Ü¶ 2 * œÄ * I * (n + 1) := by
    ext y
    simpa using deriv_const_mul (2 * œÄ * I * (n + 1)) (d := fun (x : ‚ÑÇ) ‚Ü¶ x) (x := y)
  simp_rw [eta_q_eq_cexp, h2, logDeriv_one_sub_mul_cexp_comp 1
    (g := fun x ‚Ü¶ (2 * œÄ * I * (n + 1) * x)) (by fun_prop), h3]
  simp

lemma tsum_logDeriv_eta_q (z : ‚ÑÇ) : ‚àë' n, logDeriv (fun x ‚Ü¶ 1 - eta_q n x) z =
    (2 * œÄ * I) * ‚àë' n, (n + 1) * (-eta_q n z) / (1 - eta_q n z) := by
  rw [tsum_congr (one_sub_eta_logDeriv_eq z), ‚Üê tsum_mul_left]
  grind

theorem differentiableAt_eta_of_mem_upperHalfPlaneSet {z : ‚ÑÇ} (hz : z ‚àà ‚Ñç‚Çí) :
    DifferentiableAt ‚ÑÇ eta z := by
  apply DifferentiableAt.mul (by fun_prop)
  refine (multipliableLocallyUniformlyOn_eta.hasProdLocallyUniformlyOn.differentiableOn ?_
    isOpen_upperHalfPlaneSet z hz).differentiableAt (isOpen_upperHalfPlaneSet.mem_nhds hz)
  filter_upwards with b
  simpa [Finset.prod_fn] using DifferentiableOn.finset_prod (by fun_prop)

end ModularForm
