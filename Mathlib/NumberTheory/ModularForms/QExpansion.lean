/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
module

public import Mathlib.Analysis.Complex.TaylorSeries
public import Mathlib.Analysis.Complex.UpperHalfPlane.Exp
public import Mathlib.NumberTheory.ModularForms.Basic
public import Mathlib.NumberTheory.ModularForms.Identities
public import Mathlib.RingTheory.PowerSeries.Basic
public import Mathlib.Analysis.Normed.Group.Tannery

/-!
# q-expansions of modular forms

We show that a modular form of level `Î“(n)` can be written as `Ï„ â†¦ F (ğ•¢ n Ï„)` where `F` is
analytic on the open unit disc, and `ğ•¢ n` is the parameter `Ï„ â†¦ exp (2 * I * Ï€ * Ï„ / n)`. As an
application, we show that cusp forms decay exponentially to 0 as `im Ï„ â†’ âˆ`.

We also define the `q`-expansion of a modular form, either as a power series or as a
`FormalMultilinearSeries`, and show that it converges to `f` on the upper half plane.

## Main definitions and results

* `SlashInvariantFormClass.cuspFunction`: for a level `n` slash-invariant form, this is the function
  `F` such that `f Ï„ = F (exp (2 * Ï€ * I * Ï„ / n))`, extended by a choice of limit at `0`.
* `ModularFormClass.differentiableAt_cuspFunction`: when `f` is a modular form, its `cuspFunction`
  is differentiable on the open unit disc (including at `0`).
* `ModularFormClass.qExpansion`: the `q`-expansion of a modular form (defined as the Taylor series
  of its `cuspFunction`), bundled as a `PowerSeries`.
* `ModularFormClass.hasSum_qExpansion`: the `q`-expansion evaluated at `ğ•¢ n Ï„` sums to `f Ï„`, for
  `Ï„` in the upper half plane.
* `qExpansionRingHom` defines the ring homomorphism from the graded ring of modular forms to power
  series given by taking `q`-expansions.
* `qExpansion_coeff_unique` shows that q-expansion coefficients are uniquely determined.

-/


@[expose] public noncomputable section

open ModularForm Complex Filter Function Matrix.SpecialLinearGroup
open UpperHalfPlane hiding I

open scoped Real MatrixGroups CongruenceSubgroup

variable {k : â„¤} {F : Type*} [FunLike F â„ â„‚] {Î“ : Subgroup (GL (Fin 2) â„)} {h : â„} (f : F)

local notation "Iâˆ" => comap Complex.im atTop
local notation "ğ•¢" => Periodic.qParam

namespace UpperHalfPlane

/-- The value of `f` at the cusp `âˆ` (or an arbitrary choice of value if this limit is not
well-defined). -/
def valueAtInfty (f : â„ â†’ â„‚) : â„‚ := limUnder atImInfty f

lemma IsZeroAtImInfty.valueAtInfty_eq_zero {f : â„ â†’ â„‚} (hf : IsZeroAtImInfty f) :
    valueAtInfty f = 0 :=
  hf.limUnder_eq

lemma qParam_tendsto_atImInfty {h : â„} (hh : 0 < h) :
    Tendsto (fun Ï„ : â„ â†¦ ğ•¢ h Ï„) atImInfty (nhds 0) :=
  ((Periodic.qParam_tendsto hh).mono_right nhdsWithin_le_nhds).comp tendsto_coe_atImInfty

end UpperHalfPlane

namespace SlashInvariantFormClass

theorem periodic_comp_ofComplex [SlashInvariantFormClass F Î“ k] (hÎ“ : h âˆˆ Î“.strictPeriods) :
    Periodic (f âˆ˜ ofComplex) h := by
  intro w
  by_cases! hw : 0 < im w
  Â· have : 0 < im (w + h) := by simp [hw]
    simp only [comp_apply, ofComplex_apply_of_im_pos this, ofComplex_apply_of_im_pos hw]
    convert SlashInvariantForm.vAdd_apply_of_mem_strictPeriods f âŸ¨w, hwâŸ© hÎ“ using 2
    ext
    simp [add_comm]
  Â· have : im (w + h) â‰¤ 0 := by simpa using hw
    simp [ofComplex_apply_of_im_nonpos this, ofComplex_apply_of_im_nonpos hw]

variable (h) in
/--
The analytic function `F` such that `f Ï„ = F (exp (2 * Ï€ * I * Ï„ / h))`, extended by a choice of
limit at `0`.
-/
def cuspFunction (f : â„ â†’ â„‚) : â„‚ â†’ â„‚ := Function.Periodic.cuspFunction h (f âˆ˜ ofComplex)

theorem eq_cuspFunction [SlashInvariantFormClass F Î“ k] (Ï„ : â„) (hÎ“ : h âˆˆ Î“.strictPeriods)
    (hh : h â‰  0) : cuspFunction h f (ğ•¢ h Ï„) = f Ï„ := by
  simpa using (periodic_comp_ofComplex f hÎ“).eq_cuspFunction hh Ï„

end SlashInvariantFormClass

open SlashInvariantFormClass

namespace ModularFormClass

theorem differentiableAt_comp_ofComplex [ModularFormClass F Î“ k]
      {z : â„‚} (hz : 0 < im z) :
    DifferentiableAt â„‚ (f âˆ˜ ofComplex) z :=
  mdifferentiableAt_iff_differentiableAt.mp ((holo f _).comp z (mdifferentiableAt_ofComplex hz))

theorem bounded_at_infty_comp_ofComplex [ModularFormClass F Î“ k] (hi : IsCusp OnePoint.infty Î“) :
    BoundedAtFilter Iâˆ (f âˆ˜ ofComplex) :=
  (OnePoint.isBoundedAt_infty_iff.mp (bdd_at_cusps f hi)).comp_tendsto tendsto_comap_im_ofComplex

theorem differentiableAt_cuspFunction [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) {q : â„‚} (hq : â€–qâ€– < 1) :
    DifferentiableAt â„‚ (cuspFunction h f) q := by
  rcases eq_or_ne q 0 with rfl | hq'
  Â· exact (periodic_comp_ofComplex f hÎ“).differentiableAt_cuspFunction_zero hh
      (eventually_of_mem (preimage_mem_comap (Ioi_mem_atTop 0))
        (fun _ â†¦ differentiableAt_comp_ofComplex f))
      (bounded_at_infty_comp_ofComplex f <| Î“.isCusp_of_mem_strictPeriods hh hÎ“)
  Â· exact Periodic.qParam_right_inv hh.ne' hq' â–¸
      (periodic_comp_ofComplex f hÎ“).differentiableAt_cuspFunction hh.ne'
        <| differentiableAt_comp_ofComplex _ <| Periodic.im_invQParam_pos_of_norm_lt_one hh hq hq'

lemma differentiableOn_cuspFunction_ball [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) :
    DifferentiableOn â„‚ (cuspFunction h f) (Metric.ball 0 1) :=
  fun _ hz â†¦ (differentiableAt_cuspFunction f hh hÎ“ <| mem_ball_zero_iff.mp hz)
    |>.differentiableWithinAt

lemma analyticAt_cuspFunction_zero [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) :
    AnalyticAt â„‚ (cuspFunction h f) 0 :=
  DifferentiableOn.analyticAt
    (fun q hq â†¦ (differentiableAt_cuspFunction _ hh hÎ“ hq).differentiableWithinAt)
    (by simpa [ball_zero_eq] using Metric.ball_mem_nhds (0 : â„‚) zero_lt_one)

lemma cuspFunction_apply_zero [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) :
    cuspFunction h f 0 = valueAtInfty f := by
  refine (Tendsto.limUnder_eq ?_).symm
  nth_rw 1 [â† funext fun Ï„ â†¦ eq_cuspFunction f Ï„ hÎ“ hh.ne']
  refine (analyticAt_cuspFunction_zero f hh hÎ“).continuousAt.tendsto.comp ?_
  exact qParam_tendsto_atImInfty hh

variable (h) in
/-- The `q`-expansion of a level `n` modular form, bundled as a `PowerSeries`. -/
def qExpansion (f : â„ â†’ â„‚) : PowerSeries â„‚ :=
  .mk fun m â†¦ (â†‘m.factorial)â»Â¹ * iteratedDeriv m (cuspFunction h f) 0

lemma qExpansion_coeff (f : â„ â†’ â„‚) (m : â„•) :
    (qExpansion h f).coeff m = (â†‘m.factorial)â»Â¹ * iteratedDeriv m (cuspFunction h f) 0 := by
  simp [qExpansion]

lemma qExpansion_coeff_zero [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) :
    (qExpansion h f).coeff 0 = valueAtInfty f := by
  simp [qExpansion_coeff, cuspFunction_apply_zero f hh hÎ“]

lemma hasSum_qExpansion_of_norm_lt [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) {q : â„‚} (hq : â€–qâ€– < 1) :
    HasSum (fun m : â„• â†¦ (qExpansion h f).coeff m â€¢ q ^ m) (cuspFunction h f q) := by
  convert hasSum_taylorSeries_on_ball (differentiableOn_cuspFunction_ball f hh hÎ“)
    (by simpa using hq) using 2 with m
  grind [qExpansion_coeff, sub_zero, smul_eq_mul]

@[deprecated (since := "2025-12-04")] alias hasSum_qExpansion_of_abs_lt :=
  hasSum_qExpansion_of_norm_lt

lemma hasSum_qExpansion [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) (Ï„ : â„) :
    HasSum (fun m : â„• â†¦ (qExpansion h f).coeff m â€¢ ğ•¢ h Ï„ ^ m) (f Ï„) := by
  have : 0 < 2 * Ï€ * Ï„.im / h := by positivity
  have : â€–ğ•¢ h Ï„â€– < 1 := by simpa [Periodic.qParam, Complex.norm_exp, neg_div]
  simpa [eq_cuspFunction f _ hÎ“ hh.ne'] using hasSum_qExpansion_of_norm_lt f hh hÎ“ this

variable (h) in
/--
The `q`-expansion of a modular form, bundled as a `FormalMultilinearSeries`.

TODO: Maybe get rid of this and instead define a general API for converting `PowerSeries` to
`FormalMultilinearSeries`.
-/
def qExpansionFormalMultilinearSeries : FormalMultilinearSeries â„‚ â„‚ â„‚ :=
  fun m â†¦ (qExpansion h f).coeff m â€¢ ContinuousMultilinearMap.mkPiAlgebraFin â„‚ m _

lemma qExpansionFormalMultilinearSeries_apply_norm (m : â„•) :
    â€–qExpansionFormalMultilinearSeries h f mâ€– = â€–(qExpansion h f).coeff mâ€– := by
  rw [qExpansionFormalMultilinearSeries,
    â† (ContinuousMultilinearMap.piFieldEquiv â„‚ (Fin m) â„‚).symm.norm_map]
  simp

lemma qExpansionFormalMultilinearSeries_radius [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) :
    1 â‰¤ (qExpansionFormalMultilinearSeries h f).radius := by
  refine le_of_forall_lt_imp_le_of_dense fun r hr â†¦ ?_
  lift r to NNReal using hr.ne_top
  apply FormalMultilinearSeries.le_radius_of_summable
  simp only [qExpansionFormalMultilinearSeries_apply_norm]
  rw [â† r.abs_eq]
  simp_rw [â† Real.norm_eq_abs, â† Complex.norm_real, â† norm_pow, â† norm_mul]
  exact (hasSum_qExpansion_of_norm_lt f hh hÎ“ (q := r) (by simpa using hr)).summable.norm

/-- The `q`-expansion of `f` is an `FPowerSeries` representing `cuspFunction n f`. -/
lemma hasFPowerSeries_cuspFunction [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) :
    HasFPowerSeriesOnBall (cuspFunction h f) (qExpansionFormalMultilinearSeries h f) 0 1 := by
  refine âŸ¨qExpansionFormalMultilinearSeries_radius f hh hÎ“, zero_lt_one, fun hy â†¦ ?_âŸ©
  rw [EMetric.mem_ball, edist_zero_right, enorm_eq_nnnorm, ENNReal.coe_lt_one_iff,
    â† NNReal.coe_lt_one, coe_nnnorm] at hy
  simpa [qExpansionFormalMultilinearSeries] using hasSum_qExpansion_of_norm_lt f hh hÎ“ hy

/-- The `q`-expansion coefficient can be expressed as a `circleIntegral` for any radius `0 < R < 1`.
-/
lemma qExpansion_coeff_eq_circleIntegral [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods)
    (n : â„•) {R : â„} (hR : 0 < R) (hR' : R < 1) :
    (qExpansion h f).coeff n =
      ((2 * Ï€ * I)â»Â¹ * âˆ® (z : â„‚) in C(0, R), cuspFunction h f z / z ^ (n + 1)) := by
  have := ((differentiableOn_cuspFunction_ball f hh hÎ“).mono
    (Metric.closedBall_subset_ball hR')).circleIntegral_one_div_sub_center_pow_smul hR n
  rw [smul_eq_mul, div_eq_mul_inv, mul_assoc, mul_comm, â† div_eq_iff two_pi_I_ne_zero] at this
  simp_rw [qExpansion, PowerSeries.coeff_mk, â† this, sub_zero, smul_eq_mul, one_div_mul_eq_div,
    div_eq_inv_mul]

/--
If `h` is a positive strict period of `f`, then the `q`-expansion coefficient can be expressed
as an integral along a horizontal line in the upper half-plane from `t * I` to `h + t * I`, for
any `0 < t`.
-/
lemma qExpansion_coeff_eq_intervalIntegral [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) (n : â„•)
    {t : â„} (ht : 0 < t) : (qExpansion h f).coeff n =
    1 / h * âˆ« u in (0)..h, 1 / ğ•¢ h (u + t * I) ^ n * f (âŸ¨u + t * I, by simpa using htâŸ©) := by
  -- We use a circle integral in the `q`-domain of radius `R = exp (-2 * Ï€ * t / h)`.
  let R := Real.exp (-2 * Ï€ * t / h)
  have hR0 : 0 < R := Real.exp_pos _
  have hR1 : R < 1 := Real.exp_lt_one_iff.2 <| by simpa [neg_div] using div_pos (by positivity) hh
  -- First apply `qExpansion_coeff_eq_circleIntegral` and rescale from `0 .. 2 * Ï€` to `0 .. h`.
  rw [qExpansion_coeff_eq_circleIntegral f hh hÎ“ n hR0 hR1, circleIntegral,
    show 2 * Ï€ = h * (2 * Ï€ / h) by field_simp]
  conv => enter [1, 2, 2]; rw [show 0 = 0 * (2 * Ï€ / h) by simp]
  simp_rw [â† intervalIntegral.smul_integral_comp_mul_right, real_smul, â† mul_assoc,
    â† intervalIntegral.integral_const_mul]
  -- Compare the integrands
  congr 1 with u
  let Ï„ : â„ := âŸ¨u + t * I, by simpa using htâŸ©
  have : circleMap 0 R (u * (2 * Ï€ / h)) = ğ•¢ h Ï„ := by
    simp only [circleMap, ofReal_exp, â† exp_add, zero_add, Ï„, UpperHalfPlane.coe_mk_subtype, R]
    congr 1
    push_cast
    have := I_sq
    grind
  -- now just complex exponential arithmetic to finish
  simp_rw [deriv_circleMap, this, show u + t * I = Ï„ by rfl, show âŸ¨â†‘Ï„, Ï„.2âŸ© = Ï„ by rfl,
    eq_cuspFunction f _ hÎ“ hh.ne', smul_eq_mul, pow_succ, push_cast]
  field_simp [(show ğ•¢ h Ï„ â‰  0 from Complex.exp_ne_zero _), Real.pi_ne_zero, NeZero.ne]

theorem exp_decay_sub_atImInfty [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) :
    (fun Ï„ â†¦ f Ï„ - valueAtInfty f) =O[atImInfty] fun Ï„ â†¦ Real.exp (-2 * Ï€ * Ï„.im / h) := by
  have hi : IsCusp OnePoint.infty Î“ := Î“.isCusp_of_mem_strictPeriods hh hÎ“
  convert ((periodic_comp_ofComplex f hÎ“).exp_decay_sub_of_bounded_at_inf hh
    (eventually_of_mem (preimage_mem_comap (Ioi_mem_atTop 0))
        fun _ â†¦ differentiableAt_comp_ofComplex f)
    (bounded_at_infty_comp_ofComplex f hi)).comp_tendsto tendsto_coe_atImInfty
  simp [â† cuspFunction_apply_zero f hh hÎ“, cuspFunction]

/-- Version of `exp_decay_sub_atImInfty` stating a less precise result but easier to apply in
practice (not specifying the growth rate precisely). Note that the `Fact` hypothesis is
automatically synthesized for arithmetic subgroups. -/
theorem exp_decay_sub_atImInfty' [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] [Fact (IsCusp OnePoint.infty Î“)] :
    âˆƒ c > 0, (fun Ï„ â†¦ f Ï„ - valueAtInfty f) =O[atImInfty] (fun Ï„ â†¦ Real.exp (-c * Ï„.im)) := by
  have hh : 0 < Î“.strictWidthInfty := Î“.strictWidthInfty_pos_iff.mpr Fact.out
  have hÎ“ : Î“.strictWidthInfty âˆˆ Î“.strictPeriods := Î“.strictWidthInfty_mem_strictPeriods
  refine âŸ¨2 * Ï€ / Î“.strictWidthInfty, div_pos Real.two_pi_pos hh, ?_âŸ©
  convert exp_decay_sub_atImInfty f hh hÎ“ using 3 with Ï„
  ring_nf

end ModularFormClass

open ModularFormClass

namespace UpperHalfPlane.IsZeroAtImInfty

variable {f}

lemma zero_at_infty_comp_ofComplex {f : â„ â†’ â„‚} (hf : IsZeroAtImInfty f) :
    ZeroAtFilter Iâˆ (f âˆ˜ ofComplex) :=
  hf.comp tendsto_comap_im_ofComplex

theorem cuspFunction_apply_zero {f : â„ â†’ â„‚} (hf : IsZeroAtImInfty f) (hh : 0 < h) :
    cuspFunction h f 0 = 0 :=
  Periodic.cuspFunction_zero_of_zero_at_inf hh hf.zero_at_infty_comp_ofComplex

theorem exp_decay_atImInfty [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hf : IsZeroAtImInfty f) (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) :
    f =O[atImInfty] fun Ï„ â†¦ Real.exp (-2 * Ï€ * Ï„.im / h) := by
  simpa [hf.valueAtInfty_eq_zero] using exp_decay_sub_atImInfty f hh hÎ“

/-- Version of `exp_decay_atImInfty` stating a less precise result but easier to apply in practice
(not specifying the growth rate precisely). Note that the `Fact` hypothesis is automatically
synthesized for arithmetic subgroups. -/
theorem exp_decay_atImInfty' [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] [Fact (IsCusp OnePoint.infty Î“)] (hf : IsZeroAtImInfty f) :
    âˆƒ c > 0, f =O[atImInfty] fun Ï„ â†¦ Real.exp (-c * Ï„.im) := by
  simpa [hf.valueAtInfty_eq_zero] using exp_decay_sub_atImInfty' f

end UpperHalfPlane.IsZeroAtImInfty

namespace CuspFormClass

include Î“ k -- can't be inferred from statements but shouldn't be omitted
variable [CuspFormClass F Î“ k]

theorem zero_at_infty_comp_ofComplex [Fact (IsCusp OnePoint.infty Î“)] :
    ZeroAtFilter Iâˆ (f âˆ˜ ofComplex) :=
  (zero_at_infty f).comp tendsto_comap_im_ofComplex

variable [Î“.HasDetPlusMinusOne] [DiscreteTopology Î“]

theorem cuspFunction_apply_zero (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) :
    cuspFunction h f 0 = 0 :=
  have : Fact (IsCusp OnePoint.infty Î“) := âŸ¨Î“.isCusp_of_mem_strictPeriods hh hÎ“âŸ©
  (CuspFormClass.zero_at_infty f).cuspFunction_apply_zero hh

theorem exp_decay_atImInfty (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) :
    f =O[atImInfty] fun Ï„ â†¦ Real.exp (-2 * Ï€ * Ï„.im / h) :=
  have : Fact (IsCusp OnePoint.infty Î“) := âŸ¨Î“.isCusp_of_mem_strictPeriods hh hÎ“âŸ©
  (CuspFormClass.zero_at_infty f).exp_decay_atImInfty hh hÎ“

theorem exp_decay_atImInfty' [Fact (IsCusp OnePoint.infty Î“)] :
    âˆƒ c > 0, f =O[atImInfty] fun Ï„ â†¦ Real.exp (-c * Ï„.im) :=
  (CuspFormClass.zero_at_infty f).exp_decay_atImInfty'

end CuspFormClass

section ring

open Metric Set

open scoped Topology

lemma periodic_tendsto_nhds_zero {f : â„‚ â†’ â„‚} (hcts : ContinuousAt (Periodic.cuspFunction h f) 0) :
    Tendsto (fun x â†¦ f (Periodic.invQParam h x)) (ğ“[â‰ ] 0) (ğ“ (Periodic.cuspFunction h f 0)) := by
  apply (tendsto_nhdsWithin_of_tendsto_nhds hcts.tendsto).congr'
  rw [eventuallyEq_nhdsWithin_iff, eventually_iff_exists_mem]
  refine âŸ¨ball 0 1, Metric.ball_mem_nhds _ Real.zero_lt_one , ?_âŸ©
  intro y hy hy0
  apply Function.Periodic.cuspFunction_eq_of_nonzero
  simpa only [ne_eq, mem_compl_iff, mem_singleton_iff] using hy0

theorem cuspFunction_mul_zero {f g : â„‚ â†’ â„‚} (hfcts : ContinuousAt (Periodic.cuspFunction h f) 0)
    (hgcts : ContinuousAt (Periodic.cuspFunction h g) 0) : Periodic.cuspFunction h (f * g) 0 =
    Periodic.cuspFunction h f 0 * Periodic.cuspFunction h g 0 := by
  rw [Periodic.cuspFunction, update_self]
  apply Filter.Tendsto.limUnder_eq
  exact Filter.Tendsto.mul (periodic_tendsto_nhds_zero hfcts)
    (periodic_tendsto_nhds_zero hgcts)

lemma qExpansion_mul_coeff_zero {f g : â„ â†’ â„‚} (hfcts : ContinuousAt (cuspFunction h f) 0)
    (hgcts : ContinuousAt (cuspFunction h g) 0) :
    (qExpansion h (f * g)).coeff 0 = ((qExpansion h f).coeff 0) * (qExpansion h g).coeff 0 := by
  simpa [qExpansion_coeff] using cuspFunction_mul_zero hfcts hgcts

lemma cuspFunction_mul {f g : â„ â†’ â„‚}
    (hfcts : ContinuousAt (cuspFunction h f) 0) (hgcts : ContinuousAt (cuspFunction h g) 0) :
    cuspFunction h (f * g) = cuspFunction h f * cuspFunction h g := by
  ext z
  by_cases H : z = 0
  Â· rw [H]
    simp only [Pi.mul_apply]
    apply cuspFunction_mul_zero hfcts hgcts
  Â· simp [cuspFunction, Periodic.cuspFunction, H]

lemma cuspFunction_modularForm_mul [Î“.HasDetPlusMinusOne] [DiscreteTopology Î“] {a b : â„¤}
    (f : ModularForm Î“ a) (g : ModularForm Î“ b) (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) :
    cuspFunction h (f.mul g) = cuspFunction h f * cuspFunction h g := by
  exact cuspFunction_mul (analyticAt_cuspFunction_zero f hh hÎ“).continuousAt
    (analyticAt_cuspFunction_zero g hh hÎ“).continuousAt

lemma cuspFunction_smul {f : â„ â†’ â„‚} (hfcts : ContinuousAt (cuspFunction h f) 0) (a : â„‚) :
    cuspFunction h (a â€¢ f) = a â€¢ cuspFunction h f := by
  simp only [cuspFunction, Periodic.cuspFunction] at *
  ext y
  obtain rfl | hy := eq_or_ne y 0; swap
  Â· simp [hy]
  Â· simpa using Filter.Tendsto.limUnder_eq (Filter.Tendsto.const_mul _ (by simpa using hfcts))

lemma cuspFunction_neg {f : â„ â†’ â„‚} (hfcts : ContinuousAt (cuspFunction h f) 0) :
    cuspFunction h (-f) = - cuspFunction h f := by
  simpa using cuspFunction_smul hfcts (-1)

lemma cuspFunction_add {f g : â„ â†’ â„‚} (hfcts : ContinuousAt (cuspFunction h f) 0)
    (hgcts : ContinuousAt (cuspFunction h g) 0) :
    cuspFunction h (f + g) = cuspFunction h f + cuspFunction h g := by
  simp only [cuspFunction, Periodic.cuspFunction]
  ext y
  obtain rfl | hy := eq_or_ne y 0; swap
  Â· simp [hy]
  Â· have : ((f + g) âˆ˜ â†‘ofComplex) âˆ˜ Periodic.invQParam h =
      (f âˆ˜ â†‘ofComplex) âˆ˜ Periodic.invQParam h + (g âˆ˜ â†‘ofComplex) âˆ˜ Periodic.invQParam h := by
      ext y
      simp
    simp only [Pi.add_apply, update_self] at *
    rw [this, Filter.Tendsto.limUnder_eq]
    exact Tendsto.add (tendsto_nhds_limUnder (Exists.intro _ (periodic_tendsto_nhds_zero hfcts)))
      (tendsto_nhds_limUnder (Exists.intro _ (periodic_tendsto_nhds_zero hgcts)))

lemma cuspFunction_sub {f g : â„ â†’ â„‚} (hfcts : ContinuousAt (cuspFunction h f) 0)
    (hgcts : ContinuousAt (cuspFunction h g) 0) :
    cuspFunction h (f - g) = cuspFunction h f - cuspFunction h g := by
  simp_rw [sub_eq_add_neg, â† (cuspFunction_neg hgcts)]
  apply cuspFunction_add hfcts (by simp [cuspFunction_neg hgcts, hgcts])

open Nat in
lemma qExpansion_mul {f g : â„ â†’ â„‚} {s : Set â„‚} (hsO : IsOpen s) (hs : 0 âˆˆ s)
    (hf : ContDiffOn â„‚ âŠ¤ (cuspFunction h f) s) (hg : ContDiffOn â„‚ âŠ¤ (cuspFunction h g) s) :
    qExpansion h (f * g) = qExpansion h f * qExpansion h g := by
  ext m
  induction m with
  | zero =>
    simpa using qExpansion_mul_coeff_zero
      (hf.continuousOn.continuousAt ((IsOpen.mem_nhds_iff hsO).mpr hs))
      (hg.continuousOn.continuousAt ((IsOpen.mem_nhds_iff hsO).mpr hs))
  | succ m hm =>
    have H := cuspFunction_mul (hf.continuousOn.continuousAt ((IsOpen.mem_nhds_iff hsO).mpr hs))
      (hg.continuousOn.continuousAt ((IsOpen.mem_nhds_iff hsO).mpr hs))
    have := iteratedDerivWithin_mul hsO hs (m + 1) (hf.of_le le_top) (hg.of_le le_top)
    simp_rw [â† iteratedDeriv_eq_iteratedDerivWithin (m + 1) _ hsO hs] at this
    conv at this =>
      enter [2,2,n]
      rw [â† iteratedDeriv_eq_iteratedDerivWithin n _ hsO hs,
        â† iteratedDeriv_eq_iteratedDerivWithin (m + 1 - n) _ hsO hs]
    simp only [qExpansion_coeff,  H, PowerSeries.coeff_mul, this, Nat.succ_eq_add_one]
    have h0 : ((m + 1)! : â„‚) â‰  0 := by
      simp [Nat.factorial_ne_zero (m + 1)]
    rw [inv_mul_eq_iff_eq_mulâ‚€ h0, Finset.Nat.sum_antidiagonal_eq_sum_range_succ_mk,
      Finset.mul_sum, Nat.succ_eq_add_one]
    have ht (x : â„•) : â†‘(m + 1)! *
      ((â†‘x !)â»Â¹ * iteratedDeriv x (cuspFunction h f) 0 *
        ((â†‘(m + 1 - x)!)â»Â¹ * iteratedDeriv (m + 1 - x) (cuspFunction h g) 0)) =
        (â†‘(m + 1)! *
      ((â†‘x !)â»Â¹ * ((â†‘(m + 1 - x)!)â»Â¹) * iteratedDeriv x (cuspFunction h f) 0 *
        iteratedDeriv (m + 1 - x) (cuspFunction h g) 0)) := by ring
    simp_rw [ht]
    apply Finset.sum_congr rfl (fun x hx â†¦ ?_)
    grind [Nat.cast_choose â„‚ (b := m + 1) (a := x) (by grind)]

lemma qExpansion_modularForm_mul [Î“.HasDetPlusMinusOne] [DiscreteTopology Î“] (a b : â„¤)
    (f : ModularForm Î“ a) (g : ModularForm Î“ b) (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) :
    qExpansion h (f.mul g) = qExpansion h f * qExpansion h g := by
  apply qExpansion_mul (s := Metric.ball 0 1) (isOpen_ball) (by simp)
  Â· refine DifferentiableOn.contDiffOn (fun y hy â†¦ ?_) (isOpen_ball)
    exact (differentiableAt_cuspFunction f hh hÎ“ (by simpa using hy)).differentiableWithinAt
  Â· refine DifferentiableOn.contDiffOn (fun y hy â†¦ ?_) (isOpen_ball)
    exact (differentiableAt_cuspFunction g hh hÎ“ (by simpa using hy)).differentiableWithinAt

lemma qExpansion_add {G : Type*} [FunLike G â„ â„‚] [Î“.HasDetPlusMinusOne] [DiscreteTopology Î“]
    {a b : â„¤} (f : F) [ModularFormClass F Î“ a] (g : G) [ModularFormClass G Î“ b] (hh : 0 < h)
    (hÎ“ : h âˆˆ Î“.strictPeriods) : qExpansion h (f + g) = qExpansion h f + qExpansion h g := by
  ext m
  have := cuspFunction_add (f := f) (g := g) (h := h)
   (analyticAt_cuspFunction_zero f hh hÎ“).continuousAt
   (analyticAt_cuspFunction_zero g hh hÎ“).continuousAt
  Â· simp only [qExpansion, this, PowerSeries.coeff_mk, map_add]
    rw [iteratedDeriv_add]
    Â· ring
    Â· apply ContDiffOn.contDiffAt  (s := Metric.ball 0 1) ?_ (ball_mem_nhds 0 (by simp))
      refine DifferentiableOn.contDiffOn (fun y hy â†¦ ?_) (isOpen_ball)
      exact (differentiableAt_cuspFunction f hh hÎ“ (by simpa using hy)).differentiableWithinAt
    Â· apply ContDiffOn.contDiffAt  (s := Metric.ball 0 1) ?_ (ball_mem_nhds 0 (by simp))
      refine DifferentiableOn.contDiffOn (fun y hy â†¦ ?_) (isOpen_ball)
      exact (differentiableAt_cuspFunction g hh hÎ“ (by simpa using hy)).differentiableWithinAt

lemma qExpansion_smul [Î“.HasDetPlusMinusOne] [DiscreteTopology Î“]
    (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) (a : â„‚) (f : F) [ModularFormClass F Î“ k] :
    (qExpansion h (a â€¢ f)) = (a â€¢ qExpansion h f) := by
  ext m
  simp_rw [_root_.map_smul, smul_eq_mul, qExpansion, PowerSeries.coeff_mk,
    cuspFunction_smul (analyticAt_cuspFunction_zero f hh hÎ“).continuousAt, IteratedDeriv_smul]
  grind [Pi.smul_apply, smul_eq_mul]

lemma qExpansion_neg [Î“.HasDetPlusMinusOne] [DiscreteTopology Î“]
    (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) (f : F) [ModularFormClass F Î“ k] :
    qExpansion h (-f) = - (qExpansion h f) := by
  simpa using (qExpansion_smul hh hÎ“ (-1 : â„‚) f)

lemma qExpansion_zero (hh : 0 < h) : qExpansion h 0 = 0 := by
  ext m
  have := cuspFunction_smul (f := 0) (h := h) ?_ 0
  Â· simp only [smul_zero, zero_smul, qExpansion, PowerSeries.coeff_mk, map_zero, mul_eq_zero,
      inv_eq_zero, Nat.cast_eq_zero] at *
    rw [this]
    by_cases hm : m = 0
    Â· simp only [hm, Nat.factorial_zero, one_ne_zero, iteratedDeriv_zero, Pi.zero_apply, or_true]
    Â· right
      simpa [hm] using iteratedDeriv_const (ğ•œ := â„‚) (F := â„‚) (x := 0) (c := 0) (n := m)
  apply (Function.Periodic.differentiableAt_cuspFunction_zero hh (by simp) (by simp) _).continuousAt
  simpa using ZeroAtFilter.boundedAtFilter (zero_zeroAtFilter Iâˆ)

lemma qExpansion_injective [Î“.HasDetPlusMinusOne] [DiscreteTopology Î“]
    (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) (i : â„¤) (f : ModularForm Î“ i) :
    qExpansion h f = 0 â†” f = 0 := by
  refine âŸ¨fun H â†¦ ?_, fun H â†¦ (by simp [H, qExpansion_zero hh])âŸ©
  ext z
  simp [â† (hasSum_qExpansion f hh hÎ“ z).tsum_eq, H]

/-- The qExpansion map as an additive group hom. to power series over `â„‚`. -/
def qExpansionAddHom [Î“.HasDetPlusMinusOne] [DiscreteTopology Î“] (hh : 0 < h)
    (hÎ“ : h âˆˆ Î“.strictPeriods) (i : â„¤) : ModularForm Î“ i â†’+ PowerSeries â„‚ where
  toFun f := qExpansion h f
  map_zero' := by rw [qExpansion_injective hh hÎ“]
  map_add' f g := qExpansion_add f g hh hÎ“

lemma qExpansion_one [Î“.HasDetPlusMinusOne] : qExpansion h (1 : ModularForm Î“ 0) = 1 := by
  ext m
  rw [qExpansion_coeff]
  by_cases hm : m = 0
  Â· simpa [hm, cuspFunction, Periodic.cuspFunction] using
      Filter.Tendsto.limUnder_eq tendsto_const_nhds
  Â· simp only [cuspFunction, Periodic.cuspFunction, one_coe_eq_one, Pi.one_comp,
    PowerSeries.coeff_one, hm, â†“reduceIte, mul_eq_zero, inv_eq_zero, Nat.cast_eq_zero,
    Nat.factorial_ne_zero, false_or]
    have := iteratedDeriv_const (ğ•œ := â„‚) (F := â„‚) (x := 0) (c := 1) (n := m)
    simp only [hm] at this
    convert this
    next z =>
      by_cases hz : z = 0
      Â· rw [hz]
        simpa [update_self] using Filter.Tendsto.limUnder_eq tendsto_const_nhds
      Â· simp [hz]

open scoped DirectSum in
/-- The qExpansion map as a map from the graded ring of modular forms to power series over `â„‚`. -/
def qExpansionRingHom [Î“.HasDetPlusMinusOne] [DiscreteTopology Î“] (hh : 0 < h)
    (hÎ“ : h âˆˆ Î“.strictPeriods) : (â¨ i, ModularForm Î“ i) â†’+* (PowerSeries â„‚) := by
  apply DirectSum.toSemiring (qExpansionAddHom hh hÎ“)
  Â· simpa [qExpansionAddHom, AddMonoidHom.coe_mk, ZeroHom.coe_mk] using qExpansion_one
  Â· intro a b f g
    simpa [qExpansionAddHom, AddMonoidHom.coe_mk, ZeroHom.coe_mk]
      using qExpansion_modularForm_mul a b f g hh hÎ“

@[simp]
lemma qExpansionRingHom_apply [Î“.HasDetPlusMinusOne] [DiscreteTopology Î“] (hh : 0 < h)
    (hÎ“ : h âˆˆ Î“.strictPeriods) (i : â„¤) (f : ModularForm Î“ i) :
    qExpansionRingHom hh hÎ“ (DirectSum.of _ i f) = qExpansion h f := by
  simp [qExpansionRingHom, qExpansionAddHom]

lemma qExpansion_of_mul [Î“.HasDetPlusMinusOne] [DiscreteTopology Î“] (hh : 0 < h)
    (hÎ“ : h âˆˆ Î“.strictPeriods) (a b : â„¤) (f : ModularForm Î“ a) (g : ModularForm Î“ b) :
    qExpansion h ((((DirectSum.of _ a f)) * (DirectSum.of _ b g)) (a + b)) =
    (qExpansion h f) * (qExpansion h g) := by
  have := (qExpansionRingHom hh hÎ“).map_mul (DirectSum.of _ a f) (DirectSum.of _ b g)
  simpa [DirectSum.of_mul_of]

lemma qExpansion_of_pow [Î“.HasDetPlusMinusOne] [DiscreteTopology Î“] (hh : 0 < h)
    (hÎ“ : h âˆˆ Î“.strictPeriods) (f : ModularForm Î“ k) (n : â„•) :
    qExpansion h ((((DirectSum.of _ k f)) ^ n) (n * k)) = (qExpansion h f) ^ n := by
  have := (qExpansionRingHom hh hÎ“).map_pow (DirectSum.of _ k f) n
  simpa [ DirectSum.ofPow]

lemma qExpansion_sub [Î“.HasDetPlusMinusOne] [DiscreteTopology Î“] (a b : â„¤)
    (f : ModularForm Î“ a) (g : ModularForm Î“ b) (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) :
    qExpansion h (f - g) = qExpansion h f - qExpansion h g := by
  simp_rw [sub_eq_add_neg, â† (qExpansion_neg hh hÎ“ g)]
  exact (qExpansion_add f ( -g) hh hÎ“)

end ring

section uniqueness

open Metric Topology

private lemma hasSum_cuspFunction_of_hasSum_annulus [Î“.HasDetPlusMinusOne] [DiscreteTopology Î“]
    {F : Type*} [FunLike F â„ â„‚] {k : â„¤} [ModularFormClass F Î“ k]
    (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) (c : â„• â†’ â„‚) (f : F)
    (hf : âˆ€ (Ï„ : â„), HasSum (fun m â†¦ c m â€¢ ğ•¢ h Ï„ ^ m) (f Ï„)) {q : â„‚} (hq : â€–qâ€– < 1)
    (hq1 : q â‰  0) : HasSum (fun m â†¦ c m â€¢ q ^ m) (cuspFunction h f q) := by
  have h1 := Function.Periodic.im_invQParam_pos_of_norm_lt_one hh hq hq1
  have h2 := hf âŸ¨(Periodic.invQParam h q), h1âŸ©
  have := eq_cuspFunction (h := h) f âŸ¨(Periodic.invQParam h q), h1âŸ© hÎ“ (by grind)
  simp only [smul_eq_mul, ne_eq, coe_mk_subtype] at *
  rw [Function.Periodic.qParam_right_inv (by grind) hq1] at this h2
  simpa [â† this] using h2

-- This is a bit ugly, is there a nicer way to prove this?
lemma cuspFunction_zero_eq_const_coeff {k : â„¤} {F : Type*} [FunLike F â„ â„‚]
     {h : â„} [Î“.HasDetPlusMinusOne] [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods)
     (c : â„• â†’ â„‚) (f : F) [ModularFormClass F Î“ k]
     (hf : âˆ€ (Ï„ : â„), HasSum (fun m â†¦ c m â€¢ ğ•¢ h Ï„ ^ m) (f Ï„)) : cuspFunction h f 0 = c 0 := by
  rw [cuspFunction, Function.Periodic.cuspFunction_zero_eq_limUnder_nhds_ne]
  apply Filter.Tendsto.limUnder_eq
  have (q : â„‚) := hasSum_cuspFunction_of_hasSum_annulus hh hÎ“ c f hf (q := q)
  have htt : Tendsto (fun q â†¦ âˆ‘' m, c m * q ^ m) (ğ“[â‰ ] 0) (ğ“ (c 0)) := by
    have hD := tendsto_tsum_of_dominated_convergence (ğ“• := (ğ“[â‰ ] (0 : â„‚)))
      (f := fun q : â„‚ â†¦ fun m : â„• â†¦ c m * q ^ m) (g := fun m â†¦ c m * 0 ^ m)
      (bound := fun m â†¦ â€–c mâ€– * (1 / 2 ) ^ m ) ?_ ?_ ?_
    Â· convert hD
      rw [Summable.tsum_eq_zero_add (by simpa [â† summable_nat_add_iff 1] using summable_zero)]
      simp
    Â· simpa using (this (1/2) (by norm_num)
        (by apply one_div_ne_zero; exact Ne.symm (NeZero.ne' 2))).summable.norm
    Â· exact fun k â†¦ Tendsto.const_mul (c k)
        (Tendsto.mono_left (Continuous.tendsto (continuous_pow k) 0) nhdsWithin_le_nhds)
    Â· rw [eventually_iff_exists_mem]
      use {z | (z : â„‚) â‰  0 âˆ§ â€–zâ€– < 1 / 2}
      constructor
      Â· rw [@mem_nhdsWithin_iff]
        refine âŸ¨1 / 2, by norm_num, fun y hy â†¦ by aesopâŸ©
      Â· intro y hy k
        simp only [norm_mul, norm_pow]
        gcongr
        grind
  apply htt.congr'
  rw [@eventuallyEq_nhdsWithin_iff, eventually_nhds_iff_ball]
  refine âŸ¨1, by simpa using fun y hy hy0 â†¦ (this y hy hy0).tsum_eqâŸ©

lemma hasSum_cuspFunction_of_hasSum [Î“.HasDetPlusMinusOne] [DiscreteTopology Î“] (hh : 0 < h)
    (hÎ“ : h âˆˆ Î“.strictPeriods) (c : â„• â†’ â„‚) (f : F) [ModularFormClass F Î“ k]
    (hf : âˆ€ Ï„ : â„, HasSum (fun m : â„• â†¦ (c m) â€¢ ğ•¢ h Ï„ ^ m) (f Ï„)) {q : â„‚} (hq : â€–qâ€– < 1) :
    HasSum (fun m : â„• â†¦ c m â€¢ q ^ m) (cuspFunction h f q) := by
  by_cases hq1 : q = 0
  Â· simp_rw [hq1, cuspFunction_zero_eq_const_coeff hh hÎ“ c f hf, smul_eq_mul]
    rw [Summable.hasSum_iff (by simpa [â† summable_nat_add_iff 1] using summable_zero),
      Summable.tsum_eq_zero_add (by simpa [â† summable_nat_add_iff 1] using summable_zero)]
    simp
  Â· exact hasSum_cuspFunction_of_hasSum_annulus hh hÎ“ c f hf hq hq1

private lemma qParam_onto_annulus (r h : â„) (hr : 0 < r) (hr2 : r < 1) (hh : 0 < h) :
    âˆƒ (z : â„), â€–ğ•¢ h zâ€– = r := by
  use âŸ¨Periodic.invQParam h r, ?_âŸ©
  Â· have hq := Function.Periodic.qParam_right_inv (h := h) (q := r) (by grind) (by aesop)
    simp [UpperHalfPlane.coe, hq, hr.le]
  Â· rw [Function.Periodic.im_invQParam, norm_real, Real.norm_eq_abs, Real.log_abs, mul_pos_iff]
    right
    refine âŸ¨div_neg_of_neg_of_pos (by simp [hh]) (Real.two_pi_pos), (Real.log_neg_iff hr).mpr hr2âŸ©

lemma qExpansion_coeff_unique [Î“.HasDetPlusMinusOne] [DiscreteTopology Î“] (c : â„• â†’ â„‚) (hh : 0 < h)
    (hÎ“ : h âˆˆ Î“.strictPeriods) (f : F) [ModularFormClass F Î“ k]
    (hf : âˆ€ Ï„ : â„, HasSum (fun m : â„• â†¦ (c m) â€¢ ğ•¢ h Ï„ ^ m) (f Ï„)) :
    c = (fun m â†¦ (qExpansion h f).coeff m) := by
  -- idea: use uniqueness of formal multilinear series representing a function.
  ext m
  have h0 := hasFPowerSeries_cuspFunction (h := h) f hh hÎ“
  let qExpansion2 : PowerSeries â„‚ := .mk fun m â†¦ c m
  let qq : FormalMultilinearSeries â„‚ â„‚ â„‚ :=
    fun m â†¦ (qExpansion2).coeff m â€¢ ContinuousMultilinearMap.mkPiAlgebraFin â„‚ m _
  have hqq2 (m : â„•) : â€–qq mâ€– = â€–qExpansion2.coeff mâ€– := by
    unfold qq
    rw [â† (ContinuousMultilinearMap.piFieldEquiv â„‚ (Fin m) â„‚).symm.norm_map]
    simp only [_root_.map_smul, smul_eq_mul, norm_mul,
      LinearIsometryEquiv.norm_map, ContinuousMultilinearMap.norm_mkPiAlgebraFin, mul_one]
  have H2 : HasFPowerSeriesOnBall (cuspFunction h f) qq 0 1 := by
    have H21 : 1 â‰¤ qq.radius := by
        refine le_of_forall_lt_imp_le_of_dense fun r hr â†¦ ?_
        lift r to NNReal using hr.ne_top
        apply FormalMultilinearSeries.le_radius_of_summable
        simp only [hqq2, PowerSeries.coeff_mk, qExpansion2]
        by_cases hr0 : r = 0
        Â· simpa [hr0, â† summable_nat_add_iff 1] using summable_zero
        Â· obtain âŸ¨z, hzâŸ© := qParam_onto_annulus r h ((by simp [pos_iff_ne_zero.mpr hr0] ))
            (by simpa using hr) hh
          simpa [NNReal.coe_pow, â† hz] using (summable_norm_iff.mpr (hf z).summable)
    refine âŸ¨H21 , zero_lt_one, ?_âŸ©
    intro y hy
    simp only [EMetric.mem_ball, edist_zero_right, enorm_eq_nnnorm, ENNReal.coe_lt_one_iff,
      â† NNReal.coe_lt_one, coe_nnnorm, zero_add] at hy âŠ¢
    apply (hasSum_cuspFunction_of_hasSum hh hÎ“ c f hf hy).congr
    simp [smul_eq_mul, PowerSeries.coeff_mk, qq, qExpansion2]
  have h3 : HasFPowerSeriesAt (cuspFunction h f) qq 0 := by
    use 1
  have h4 : HasFPowerSeriesAt (cuspFunction h f) (qExpansionFormalMultilinearSeries h f) 0 := by
    use 1
  have := FormalMultilinearSeries.ext_iff.mp (HasFPowerSeriesAt.eq_formalMultilinearSeries h3 h4) m
  simp only [PowerSeries.coeff_mk, qExpansionFormalMultilinearSeries, qq, qExpansion2] at this
  have htv : (c m â€¢ ContinuousMultilinearMap.mkPiAlgebraFin â„‚ m â„‚).toFun = ((PowerSeries.coeff m)
    (qExpansion h f) â€¢ ContinuousMultilinearMap.mkPiAlgebraFin â„‚ m â„‚).toFun := by
    rw [this]
  simpa [Pi.natCast_def, qExpansion2, qq] using (congrFun htv m)


end uniqueness
