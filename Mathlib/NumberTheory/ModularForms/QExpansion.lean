/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
module

public import Mathlib.Analysis.Complex.TaylorSeries
public import Mathlib.Analysis.Complex.UpperHalfPlane.Exp
public import Mathlib.NumberTheory.ModularForms.Basic
public import Mathlib.NumberTheory.ModularForms.Identities
public import Mathlib.RingTheory.PowerSeries.Basic
public import Mathlib.Analysis.Normed.Group.Tannery

/-!
# q-expansions of modular forms

We show that a modular form of level `Œì(n)` can be written as `œÑ ‚Ü¶ F (ùï¢ n œÑ)` where `F` is
analytic on the open unit disc, and `ùï¢ n` is the parameter `œÑ ‚Ü¶ exp (2 * I * œÄ * œÑ / n)`. As an
application, we show that cusp forms decay exponentially to 0 as `im œÑ ‚Üí ‚àû`.

We also define the `q`-expansion of a modular form, either as a power series or as a
`FormalMultilinearSeries`, and show that it converges to `f` on the upper half plane.

## Main definitions and results

* `SlashInvariantFormClass.cuspFunction`: for a level `n` slash-invariant form, this is the function
  `F` such that `f œÑ = F (exp (2 * œÄ * I * œÑ / n))`, extended by a choice of limit at `0`.
* `ModularFormClass.differentiableAt_cuspFunction`: when `f` is a modular form, its `cuspFunction`
  is differentiable on the open unit disc (including at `0`).
* `ModularFormClass.qExpansion`: the `q`-expansion of a modular form (defined as the Taylor series
  of its `cuspFunction`), bundled as a `PowerSeries`.
* `ModularFormClass.hasSum_qExpansion`: the `q`-expansion evaluated at `ùï¢ n œÑ` sums to `f œÑ`, for
  `œÑ` in the upper half plane.

## TO DO:

* define the `q`-expansion map on modular form spaces as a linear map (or even a ring hom from
  the graded ring of all modular forms?)
-/


@[expose] public noncomputable section

open ModularForm Complex Filter Function Matrix.SpecialLinearGroup
open UpperHalfPlane hiding I

open scoped Real MatrixGroups CongruenceSubgroup

variable {k : ‚Ñ§} {F : Type*} [FunLike F ‚Ñç ‚ÑÇ] {Œì : Subgroup (GL (Fin 2) ‚Ñù)} {h : ‚Ñù} (f : F)

local notation "I‚àû" => comap Complex.im atTop
local notation "ùï¢" => Periodic.qParam

namespace UpperHalfPlane

/-- The value of `f` at the cusp `‚àû` (or an arbitrary choice of value if this limit is not
well-defined). -/
def valueAtInfty (f : ‚Ñç ‚Üí ‚ÑÇ) : ‚ÑÇ := limUnder atImInfty f

lemma IsZeroAtImInfty.valueAtInfty_eq_zero {f : ‚Ñç ‚Üí ‚ÑÇ} (hf : IsZeroAtImInfty f) :
    valueAtInfty f = 0 :=
  hf.limUnder_eq

lemma qParam_tendsto_atImInfty {h : ‚Ñù} (hh : 0 < h) :
    Tendsto (fun œÑ : ‚Ñç ‚Ü¶ ùï¢ h œÑ) atImInfty (nhds 0) :=
  ((Periodic.qParam_tendsto hh).mono_right nhdsWithin_le_nhds).comp tendsto_coe_atImInfty

end UpperHalfPlane

namespace SlashInvariantFormClass

theorem periodic_comp_ofComplex [SlashInvariantFormClass F Œì k] (hŒì : h ‚àà Œì.strictPeriods) :
    Periodic (f ‚àò ofComplex) h := by
  intro w
  by_cases! hw : 0 < im w
  ¬∑ have : 0 < im (w + h) := by simp [hw]
    simp only [comp_apply, ofComplex_apply_of_im_pos this, ofComplex_apply_of_im_pos hw]
    convert SlashInvariantForm.vAdd_apply_of_mem_strictPeriods f ‚ü®w, hw‚ü© hŒì using 2
    ext
    simp [add_comm]
  ¬∑ have : im (w + h) ‚â§ 0 := by simpa using hw
    simp [ofComplex_apply_of_im_nonpos this, ofComplex_apply_of_im_nonpos hw]

variable (h) in
/--
The analytic function `F` such that `f œÑ = F (exp (2 * œÄ * I * œÑ / h))`, extended by a choice of
limit at `0`.
-/
def cuspFunction (f : ‚Ñç ‚Üí ‚ÑÇ) : ‚ÑÇ ‚Üí ‚ÑÇ := Function.Periodic.cuspFunction h (f ‚àò ofComplex)

theorem eq_cuspFunction [SlashInvariantFormClass F Œì k] (œÑ : ‚Ñç) (hŒì : h ‚àà Œì.strictPeriods)
    (hh : h ‚â† 0) : cuspFunction h f (ùï¢ h œÑ) = f œÑ := by
  simpa using (periodic_comp_ofComplex f hŒì).eq_cuspFunction hh œÑ

end SlashInvariantFormClass

open SlashInvariantFormClass

namespace ModularFormClass

theorem differentiableAt_comp_ofComplex [ModularFormClass F Œì k]
      {z : ‚ÑÇ} (hz : 0 < im z) :
    DifferentiableAt ‚ÑÇ (f ‚àò ofComplex) z :=
  mdifferentiableAt_iff_differentiableAt.mp ((holo f _).comp z (mdifferentiableAt_ofComplex hz))

theorem bounded_at_infty_comp_ofComplex [ModularFormClass F Œì k] (hi : IsCusp OnePoint.infty Œì) :
    BoundedAtFilter I‚àû (f ‚àò ofComplex) :=
  (OnePoint.isBoundedAt_infty_iff.mp (bdd_at_cusps f hi)).comp_tendsto tendsto_comap_im_ofComplex

theorem differentiableAt_cuspFunction [ModularFormClass F Œì k] [Œì.HasDetPlusMinusOne]
    [DiscreteTopology Œì] (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) {q : ‚ÑÇ} (hq : ‚Äñq‚Äñ < 1) :
    DifferentiableAt ‚ÑÇ (cuspFunction h f) q := by
  rcases eq_or_ne q 0 with rfl | hq'
  ¬∑ exact (periodic_comp_ofComplex f hŒì).differentiableAt_cuspFunction_zero hh
      (eventually_of_mem (preimage_mem_comap (Ioi_mem_atTop 0))
        (fun _ ‚Ü¶ differentiableAt_comp_ofComplex f))
      (bounded_at_infty_comp_ofComplex f <| Œì.isCusp_of_mem_strictPeriods hh hŒì)
  ¬∑ exact Periodic.qParam_right_inv hh.ne' hq' ‚ñ∏
      (periodic_comp_ofComplex f hŒì).differentiableAt_cuspFunction hh.ne'
        <| differentiableAt_comp_ofComplex _ <| Periodic.im_invQParam_pos_of_norm_lt_one hh hq hq'

lemma differentiableOn_cuspFunction_ball [ModularFormClass F Œì k] [Œì.HasDetPlusMinusOne]
    [DiscreteTopology Œì] (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) :
    DifferentiableOn ‚ÑÇ (cuspFunction h f) (Metric.ball 0 1) :=
  fun _ hz ‚Ü¶ (differentiableAt_cuspFunction f hh hŒì <| mem_ball_zero_iff.mp hz)
    |>.differentiableWithinAt

lemma analyticAt_cuspFunction_zero [ModularFormClass F Œì k] [Œì.HasDetPlusMinusOne]
    [DiscreteTopology Œì] (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) :
    AnalyticAt ‚ÑÇ (cuspFunction h f) 0 :=
  DifferentiableOn.analyticAt
    (fun q hq ‚Ü¶ (differentiableAt_cuspFunction _ hh hŒì hq).differentiableWithinAt)
    (by simpa [ball_zero_eq] using Metric.ball_mem_nhds (0 : ‚ÑÇ) zero_lt_one)

lemma cuspFunction_apply_zero [ModularFormClass F Œì k] [Œì.HasDetPlusMinusOne]
    [DiscreteTopology Œì] (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) :
    cuspFunction h f 0 = valueAtInfty f := by
  refine (Tendsto.limUnder_eq ?_).symm
  nth_rw 1 [‚Üê funext fun œÑ ‚Ü¶ eq_cuspFunction f œÑ hŒì hh.ne']
  refine (analyticAt_cuspFunction_zero f hh hŒì).continuousAt.tendsto.comp ?_
  exact qParam_tendsto_atImInfty hh

variable (h) in
/-- The `q`-expansion of a level `n` modular form, bundled as a `PowerSeries`. -/
def qExpansion (f : ‚Ñç ‚Üí ‚ÑÇ) : PowerSeries ‚ÑÇ :=
  .mk fun m ‚Ü¶ (‚Üëm.factorial)‚Åª¬π * iteratedDeriv m (cuspFunction h f) 0

lemma qExpansion_coeff (f : ‚Ñç ‚Üí ‚ÑÇ) (m : ‚Ñï) :
    (qExpansion h f).coeff m = (‚Üëm.factorial)‚Åª¬π * iteratedDeriv m (cuspFunction h f) 0 := by
  simp [qExpansion]

lemma qExpansion_coeff_zero [ModularFormClass F Œì k] [Œì.HasDetPlusMinusOne]
    [DiscreteTopology Œì] (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) :
    (qExpansion h f).coeff 0 = valueAtInfty f := by
  simp [qExpansion_coeff, cuspFunction_apply_zero f hh hŒì]

lemma hasSum_qExpansion_of_norm_lt [ModularFormClass F Œì k] [Œì.HasDetPlusMinusOne]
    [DiscreteTopology Œì] (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) {q : ‚ÑÇ} (hq : ‚Äñq‚Äñ < 1) :
    HasSum (fun m : ‚Ñï ‚Ü¶ (qExpansion h f).coeff m ‚Ä¢ q ^ m) (cuspFunction h f q) := by
  convert hasSum_taylorSeries_on_ball (differentiableOn_cuspFunction_ball f hh hŒì)
    (by simpa using hq) using 2 with m
  grind [qExpansion_coeff, sub_zero, smul_eq_mul]

@[deprecated (since := "2025-12-04")] alias hasSum_qExpansion_of_abs_lt :=
  hasSum_qExpansion_of_norm_lt

lemma hasSum_qExpansion [ModularFormClass F Œì k] [Œì.HasDetPlusMinusOne]
    [DiscreteTopology Œì] (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) (œÑ : ‚Ñç) :
    HasSum (fun m : ‚Ñï ‚Ü¶ (qExpansion h f).coeff m ‚Ä¢ ùï¢ h œÑ ^ m) (f œÑ) := by
  have : 0 < 2 * œÄ * œÑ.im / h := by positivity
  have : ‚Äñùï¢ h œÑ‚Äñ < 1 := by simpa [Periodic.qParam, Complex.norm_exp, neg_div]
  simpa [eq_cuspFunction f _ hŒì hh.ne'] using hasSum_qExpansion_of_norm_lt f hh hŒì this

variable (h) in
/--
The `q`-expansion of a modular form, bundled as a `FormalMultilinearSeries`.

TODO: Maybe get rid of this and instead define a general API for converting `PowerSeries` to
`FormalMultilinearSeries`.
-/
def qExpansionFormalMultilinearSeries : FormalMultilinearSeries ‚ÑÇ ‚ÑÇ ‚ÑÇ :=
  fun m ‚Ü¶ (qExpansion h f).coeff m ‚Ä¢ ContinuousMultilinearMap.mkPiAlgebraFin ‚ÑÇ m _

lemma qExpansionFormalMultilinearSeries_apply_norm (m : ‚Ñï) :
    ‚ÄñqExpansionFormalMultilinearSeries h f m‚Äñ = ‚Äñ(qExpansion h f).coeff m‚Äñ := by
  rw [qExpansionFormalMultilinearSeries,
    ‚Üê (ContinuousMultilinearMap.piFieldEquiv ‚ÑÇ (Fin m) ‚ÑÇ).symm.norm_map]
  simp

lemma qExpansionFormalMultilinearSeries_radius [ModularFormClass F Œì k] [Œì.HasDetPlusMinusOne]
    [DiscreteTopology Œì] (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) :
    1 ‚â§ (qExpansionFormalMultilinearSeries h f).radius := by
  refine le_of_forall_lt_imp_le_of_dense fun r hr ‚Ü¶ ?_
  lift r to NNReal using hr.ne_top
  apply FormalMultilinearSeries.le_radius_of_summable
  simp only [qExpansionFormalMultilinearSeries_apply_norm]
  rw [‚Üê r.abs_eq]
  simp_rw [‚Üê Real.norm_eq_abs, ‚Üê Complex.norm_real, ‚Üê norm_pow, ‚Üê norm_mul]
  exact (hasSum_qExpansion_of_norm_lt f hh hŒì (q := r) (by simpa using hr)).summable.norm

/-- The `q`-expansion of `f` is an `FPowerSeries` representing `cuspFunction n f`. -/
lemma hasFPowerSeries_cuspFunction [ModularFormClass F Œì k] [Œì.HasDetPlusMinusOne]
    [DiscreteTopology Œì] (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) :
    HasFPowerSeriesOnBall (cuspFunction h f) (qExpansionFormalMultilinearSeries h f) 0 1 := by
  refine ‚ü®qExpansionFormalMultilinearSeries_radius f hh hŒì, zero_lt_one, fun hy ‚Ü¶ ?_‚ü©
  rw [EMetric.mem_ball, edist_zero_right, enorm_eq_nnnorm, ENNReal.coe_lt_one_iff,
    ‚Üê NNReal.coe_lt_one, coe_nnnorm] at hy
  simpa [qExpansionFormalMultilinearSeries] using hasSum_qExpansion_of_norm_lt f hh hŒì hy

/-- The `q`-expansion coefficient can be expressed as a `circleIntegral` for any radius `0 < R < 1`.
-/
lemma qExpansion_coeff_eq_circleIntegral [ModularFormClass F Œì k] [Œì.HasDetPlusMinusOne]
    [DiscreteTopology Œì] (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods)
    (n : ‚Ñï) {R : ‚Ñù} (hR : 0 < R) (hR' : R < 1) :
    (qExpansion h f).coeff n =
      ((2 * œÄ * I)‚Åª¬π * ‚àÆ (z : ‚ÑÇ) in C(0, R), cuspFunction h f z / z ^ (n + 1)) := by
  have := ((differentiableOn_cuspFunction_ball f hh hŒì).mono
    (Metric.closedBall_subset_ball hR')).circleIntegral_one_div_sub_center_pow_smul hR n
  rw [smul_eq_mul, div_eq_mul_inv, mul_assoc, mul_comm, ‚Üê div_eq_iff two_pi_I_ne_zero] at this
  simp_rw [qExpansion, PowerSeries.coeff_mk, ‚Üê this, sub_zero, smul_eq_mul, one_div_mul_eq_div,
    div_eq_inv_mul]

/--
If `h` is a positive strict period of `f`, then the `q`-expansion coefficient can be expressed
as an integral along a horizontal line in the upper half-plane from `t * I` to `h + t * I`, for
any `0 < t`.
-/
lemma qExpansion_coeff_eq_intervalIntegral [ModularFormClass F Œì k] [Œì.HasDetPlusMinusOne]
    [DiscreteTopology Œì] (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) (n : ‚Ñï)
    {t : ‚Ñù} (ht : 0 < t) : (qExpansion h f).coeff n =
    1 / h * ‚à´ u in (0)..h, 1 / ùï¢ h (u + t * I) ^ n * f (‚ü®u + t * I, by simpa using ht‚ü©) := by
  -- We use a circle integral in the `q`-domain of radius `R = exp (-2 * œÄ * t / h)`.
  let R := Real.exp (-2 * œÄ * t / h)
  have hR0 : 0 < R := Real.exp_pos _
  have hR1 : R < 1 := Real.exp_lt_one_iff.2 <| by simpa [neg_div] using div_pos (by positivity) hh
  -- First apply `qExpansion_coeff_eq_circleIntegral` and rescale from `0 .. 2 * œÄ` to `0 .. h`.
  rw [qExpansion_coeff_eq_circleIntegral f hh hŒì n hR0 hR1, circleIntegral,
    show 2 * œÄ = h * (2 * œÄ / h) by field_simp]
  conv => enter [1, 2, 2]; rw [show 0 = 0 * (2 * œÄ / h) by simp]
  simp_rw [‚Üê intervalIntegral.smul_integral_comp_mul_right, real_smul, ‚Üê mul_assoc,
    ‚Üê intervalIntegral.integral_const_mul]
  -- Compare the integrands
  congr 1 with u
  let œÑ : ‚Ñç := ‚ü®u + t * I, by simpa using ht‚ü©
  have : circleMap 0 R (u * (2 * œÄ / h)) = ùï¢ h œÑ := by
    simp only [circleMap, ofReal_exp, ‚Üê exp_add, zero_add, œÑ, UpperHalfPlane.coe_mk_subtype, R]
    congr 1
    push_cast
    have := I_sq
    grind
  -- now just complex exponential arithmetic to finish
  simp_rw [deriv_circleMap, this, show u + t * I = œÑ by rfl, show ‚ü®‚ÜëœÑ, œÑ.2‚ü© = œÑ by rfl,
    eq_cuspFunction f _ hŒì hh.ne', smul_eq_mul, pow_succ, push_cast]
  field_simp [(show ùï¢ h œÑ ‚â† 0 from Complex.exp_ne_zero _), Real.pi_ne_zero, NeZero.ne]

theorem exp_decay_sub_atImInfty [ModularFormClass F Œì k] [Œì.HasDetPlusMinusOne]
    [DiscreteTopology Œì] (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) :
    (fun œÑ ‚Ü¶ f œÑ - valueAtInfty f) =O[atImInfty] fun œÑ ‚Ü¶ Real.exp (-2 * œÄ * œÑ.im / h) := by
  have hi : IsCusp OnePoint.infty Œì := Œì.isCusp_of_mem_strictPeriods hh hŒì
  convert ((periodic_comp_ofComplex f hŒì).exp_decay_sub_of_bounded_at_inf hh
    (eventually_of_mem (preimage_mem_comap (Ioi_mem_atTop 0))
        fun _ ‚Ü¶ differentiableAt_comp_ofComplex f)
    (bounded_at_infty_comp_ofComplex f hi)).comp_tendsto tendsto_coe_atImInfty
  simp [‚Üê cuspFunction_apply_zero f hh hŒì, cuspFunction]

/-- Version of `exp_decay_sub_atImInfty` stating a less precise result but easier to apply in
practice (not specifying the growth rate precisely). Note that the `Fact` hypothesis is
automatically synthesized for arithmetic subgroups. -/
theorem exp_decay_sub_atImInfty' [ModularFormClass F Œì k] [Œì.HasDetPlusMinusOne]
    [DiscreteTopology Œì] [Fact (IsCusp OnePoint.infty Œì)] :
    ‚àÉ c > 0, (fun œÑ ‚Ü¶ f œÑ - valueAtInfty f) =O[atImInfty] (fun œÑ ‚Ü¶ Real.exp (-c * œÑ.im)) := by
  have hh : 0 < Œì.strictWidthInfty := Œì.strictWidthInfty_pos_iff.mpr Fact.out
  have hŒì : Œì.strictWidthInfty ‚àà Œì.strictPeriods := Œì.strictWidthInfty_mem_strictPeriods
  refine ‚ü®2 * œÄ / Œì.strictWidthInfty, div_pos Real.two_pi_pos hh, ?_‚ü©
  convert exp_decay_sub_atImInfty f hh hŒì using 3 with œÑ
  ring_nf

end ModularFormClass

open ModularFormClass

namespace UpperHalfPlane.IsZeroAtImInfty

variable {f}

lemma zero_at_infty_comp_ofComplex {f : ‚Ñç ‚Üí ‚ÑÇ} (hf : IsZeroAtImInfty f) :
    ZeroAtFilter I‚àû (f ‚àò ofComplex) :=
  hf.comp tendsto_comap_im_ofComplex

theorem cuspFunction_apply_zero {f : ‚Ñç ‚Üí ‚ÑÇ} (hf : IsZeroAtImInfty f) (hh : 0 < h) :
    cuspFunction h f 0 = 0 :=
  Periodic.cuspFunction_zero_of_zero_at_inf hh hf.zero_at_infty_comp_ofComplex

theorem exp_decay_atImInfty [ModularFormClass F Œì k] [Œì.HasDetPlusMinusOne]
    [DiscreteTopology Œì] (hf : IsZeroAtImInfty f) (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) :
    f =O[atImInfty] fun œÑ ‚Ü¶ Real.exp (-2 * œÄ * œÑ.im / h) := by
  simpa [hf.valueAtInfty_eq_zero] using exp_decay_sub_atImInfty f hh hŒì

/-- Version of `exp_decay_atImInfty` stating a less precise result but easier to apply in practice
(not specifying the growth rate precisely). Note that the `Fact` hypothesis is automatically
synthesized for arithmetic subgroups. -/
theorem exp_decay_atImInfty' [ModularFormClass F Œì k] [Œì.HasDetPlusMinusOne]
    [DiscreteTopology Œì] [Fact (IsCusp OnePoint.infty Œì)] (hf : IsZeroAtImInfty f) :
    ‚àÉ c > 0, f =O[atImInfty] fun œÑ ‚Ü¶ Real.exp (-c * œÑ.im) := by
  simpa [hf.valueAtInfty_eq_zero] using exp_decay_sub_atImInfty' f

end UpperHalfPlane.IsZeroAtImInfty

namespace CuspFormClass

include Œì k -- can't be inferred from statements but shouldn't be omitted
variable [CuspFormClass F Œì k]

theorem zero_at_infty_comp_ofComplex [Fact (IsCusp OnePoint.infty Œì)] :
    ZeroAtFilter I‚àû (f ‚àò ofComplex) :=
  (zero_at_infty f).comp tendsto_comap_im_ofComplex

variable [Œì.HasDetPlusMinusOne] [DiscreteTopology Œì]

theorem cuspFunction_apply_zero (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) :
    cuspFunction h f 0 = 0 :=
  have : Fact (IsCusp OnePoint.infty Œì) := ‚ü®Œì.isCusp_of_mem_strictPeriods hh hŒì‚ü©
  (CuspFormClass.zero_at_infty f).cuspFunction_apply_zero hh

theorem exp_decay_atImInfty (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) :
    f =O[atImInfty] fun œÑ ‚Ü¶ Real.exp (-2 * œÄ * œÑ.im / h) :=
  have : Fact (IsCusp OnePoint.infty Œì) := ‚ü®Œì.isCusp_of_mem_strictPeriods hh hŒì‚ü©
  (CuspFormClass.zero_at_infty f).exp_decay_atImInfty hh hŒì

theorem exp_decay_atImInfty' [Fact (IsCusp OnePoint.infty Œì)] :
    ‚àÉ c > 0, f =O[atImInfty] fun œÑ ‚Ü¶ Real.exp (-c * œÑ.im) :=
  (CuspFormClass.zero_at_infty f).exp_decay_atImInfty'

end CuspFormClass

section ring

open Metric Set

open scoped Topology

lemma periodic_tendto_ndhs_zero {f : ‚ÑÇ ‚Üí ‚ÑÇ} (hcts : ContinuousAt (Periodic.cuspFunction h f) 0) :
    Tendsto (fun x ‚Ü¶ f (Periodic.invQParam h x)) (ùìù[‚â†] 0) (ùìù (Periodic.cuspFunction h f 0)) := by
  apply (tendsto_nhdsWithin_of_tendsto_nhds hcts.tendsto).congr'
  rw [eventuallyEq_nhdsWithin_iff, eventually_iff_exists_mem]
  refine ‚ü®ball 0 1, Metric.ball_mem_nhds _ Real.zero_lt_one , ?_‚ü©
  intro y hy hy0
  apply Function.Periodic.cuspFunction_eq_of_nonzero
  simpa only [ne_eq, mem_compl_iff, mem_singleton_iff] using hy0

theorem cuspFunction_mul_zero {f g : ‚ÑÇ ‚Üí ‚ÑÇ} (hfcts : ContinuousAt (Periodic.cuspFunction h f) 0)
    (hgcts : ContinuousAt (Periodic.cuspFunction h g) 0) : Periodic.cuspFunction h (f * g) 0 =
    Periodic.cuspFunction h f 0 * Periodic.cuspFunction h g 0 := by
  rw [Periodic.cuspFunction, update_self]
  apply Filter.Tendsto.limUnder_eq
  exact Filter.Tendsto.mul (periodic_tendto_ndhs_zero hfcts)
    (periodic_tendto_ndhs_zero hgcts)

lemma qExpansion_mul_coeff_zero {f g : ‚Ñç ‚Üí ‚ÑÇ} (hfcts : ContinuousAt (cuspFunction h f) 0)
    (hgcts : ContinuousAt (cuspFunction h g) 0) :
    (qExpansion h (f * g)).coeff 0 = ((qExpansion h f).coeff 0) * (qExpansion h g).coeff 0 := by
    simpa [qExpansion_coeff] using cuspFunction_mul_zero hfcts hgcts

lemma cuspFunction_mul' {f g : ‚Ñç ‚Üí ‚ÑÇ} (hfcts : ContinuousAt (cuspFunction h f) 0)
    (hgcts : ContinuousAt (cuspFunction h g) 0) :
    cuspFunction h (f * g) = cuspFunction h f * cuspFunction h g := by
  ext z
  by_cases H : z = 0
  ¬∑ rw [H]
    simp only [Pi.mul_apply]
    apply cuspFunction_mul_zero hfcts hgcts
  ¬∑ simp [cuspFunction, Periodic.cuspFunction, H]

lemma cuspFunction_mul [Œì.HasDetPlusMinusOne] [DiscreteTopology Œì] (a b : ‚Ñ§) (f : ModularForm Œì a)
    (g : ModularForm Œì b) (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) :
    cuspFunction h (f.mul g) = cuspFunction h f * cuspFunction h g := by
  exact cuspFunction_mul' (analyticAt_cuspFunction_zero f hh hŒì).continuousAt
    (analyticAt_cuspFunction_zero g hh hŒì).continuousAt

lemma iteratedDeriv_eq_iteratedDerivWithin (n : ‚Ñï) (f : ‚ÑÇ ‚Üí ‚ÑÇ) (s : Set ‚ÑÇ) (hs : IsOpen s)
    (z : ‚ÑÇ) (hz : z ‚àà s) : iteratedDeriv n f z = iteratedDerivWithin n f s z := by
  simp_rw [‚Üê iteratedDerivWithin_univ, iteratedDerivWithin]
  rw [iteratedFDerivWithin_congr_set]
  apply EventuallyEq.symm
  simpa [eventuallyEq_univ] using IsOpen.mem_nhds hs hz

open Nat in
lemma qExpansion_mul' {f g : ‚Ñç ‚Üí ‚ÑÇ} (s : Set ‚ÑÇ) (hsO : IsOpen s) (hs : 0 ‚àà s)
    (hf : ContDiffOn ‚ÑÇ ‚ä§ (cuspFunction h f) s) (hg : ContDiffOn ‚ÑÇ ‚ä§ (cuspFunction h g) s) :
    qExpansion h (f * g) = qExpansion h f * qExpansion h g := by
  ext m
  induction m with
  | zero =>
    simpa using qExpansion_mul_coeff_zero
      (hf.continuousOn.continuousAt ((IsOpen.mem_nhds_iff hsO).mpr hs))
      (hg.continuousOn.continuousAt ((IsOpen.mem_nhds_iff hsO).mpr hs))
  | succ m hm =>
    have H := cuspFunction_mul' (hf.continuousOn.continuousAt ((IsOpen.mem_nhds_iff hsO).mpr hs))
      (hg.continuousOn.continuousAt ((IsOpen.mem_nhds_iff hsO).mpr hs))
    simp only [qExpansion_coeff,  H,
      PowerSeries.coeff_mul] at *
    have := iteratedDerivWithin_mul hsO hs (m + 1) (hf.of_le le_top) (hg.of_le le_top)
    ¬∑ simp_rw [‚Üê iteratedDeriv_eq_iteratedDerivWithin (m + 1) _ s hsO 0 hs] at this
      conv at this =>
        enter [2,2,n]
        rw [‚Üê iteratedDeriv_eq_iteratedDerivWithin n _ s hsO 0 hs,
          ‚Üê iteratedDeriv_eq_iteratedDerivWithin (m + 1 - n) _ s hsO 0 hs]
      rw [this, Nat.succ_eq_add_one]
      have h0 : ((m + 1)! : ‚ÑÇ) ‚â† 0 := by
        norm_cast
        exact Nat.factorial_ne_zero (m + 1)
      rw [inv_mul_eq_iff_eq_mul‚ÇÄ h0, Finset.Nat.sum_antidiagonal_eq_sum_range_succ_mk,
        Finset.mul_sum, Nat.succ_eq_add_one]
      have ht (x : ‚Ñï) : ‚Üë(m + 1)! *
        ((‚Üëx !)‚Åª¬π * iteratedDeriv x (cuspFunction h f) 0 *
          ((‚Üë(m + 1 - x)!)‚Åª¬π * iteratedDeriv (m + 1 - x) (cuspFunction h g) 0)) =
          (‚Üë(m + 1)! *
        ((‚Üëx !)‚Åª¬π * ((‚Üë(m + 1 - x)!)‚Åª¬π) * iteratedDeriv x (cuspFunction h f) 0 *
          iteratedDeriv (m + 1 - x) (cuspFunction h g) 0)) := by ring
      conv =>
        enter [2,2,x]
        rw [ht x]
      apply Finset.sum_congr rfl (fun x hx ‚Ü¶ ?_)
      simp_rw [‚Üê mul_assoc]
      congr
      simp only [Finset.mem_range] at hx
      rw [Nat.cast_choose ‚ÑÇ (b := m + 1) (a := x)]
      ¬∑ field_simp
      ¬∑ omega

lemma qExpansion_mul [Œì.HasDetPlusMinusOne] [DiscreteTopology Œì] (a b : ‚Ñ§)
    (f : ModularForm Œì a) (g : ModularForm Œì b) (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) :
    qExpansion h (f.mul g) = qExpansion h f * qExpansion h g := by
  apply qExpansion_mul' (Metric.ball 0 1) (isOpen_ball) (by simp)
  ¬∑ refine DifferentiableOn.contDiffOn (fun y hy ‚Ü¶ ?_) (isOpen_ball)
    exact (differentiableAt_cuspFunction f hh hŒì (by simpa using hy)).differentiableWithinAt
  ¬∑ refine DifferentiableOn.contDiffOn (fun y hy ‚Ü¶ ?_) (isOpen_ball)
    exact (differentiableAt_cuspFunction g hh hŒì (by simpa using hy)).differentiableWithinAt

lemma cuspFunction_sub {f g : ‚Ñç ‚Üí ‚ÑÇ} (hfcts : ContinuousAt (cuspFunction h f) 0)
    (hgcts : ContinuousAt (cuspFunction h g) 0) :
    cuspFunction h (f - g) = cuspFunction h f - cuspFunction h g := by
  simp only [cuspFunction, Periodic.cuspFunction]
  ext y
  obtain rfl | hy := eq_or_ne y 0; swap
  ¬∑ simp [hy]
  ¬∑ have : ((f - g) ‚àò ‚ÜëofComplex) ‚àò Periodic.invQParam h = (f ‚àò ‚ÜëofComplex) ‚àò Periodic.invQParam h
        - (g ‚àò ‚ÜëofComplex) ‚àò Periodic.invQParam h := by
      ext y
      simp
    simp only [Pi.sub_apply, update_self] at *
    rw [this, Filter.Tendsto.limUnder_eq]
    apply Tendsto.sub
    ¬∑ exact tendsto_nhds_limUnder (Exists.intro _ ( periodic_tendto_ndhs_zero hfcts))
    ¬∑ exact tendsto_nhds_limUnder (Exists.intro _ ( periodic_tendto_ndhs_zero hgcts))

lemma cuspFunction_smul {f : ‚Ñç ‚Üí ‚ÑÇ} (hfcts : ContinuousAt (cuspFunction h f) 0) (a : ‚ÑÇ) :
    cuspFunction h (a ‚Ä¢ f) = a ‚Ä¢ cuspFunction h f := by
  simp only [cuspFunction, Periodic.cuspFunction] at *
  ext y
  obtain rfl | hy := eq_or_ne y 0; swap
  ¬∑ simp [hy]
  ¬∑ simp only [update_self] at *
    rw [ Filter.Tendsto.limUnder_eq]
    apply Filter.Tendsto.const_mul
    simpa using hfcts

lemma cuspFunction_add {f g : ‚Ñç ‚Üí ‚ÑÇ} (hfcts : ContinuousAt (cuspFunction h f) 0)
    (hgcts : ContinuousAt (cuspFunction h g) 0) :
    cuspFunction h (f + g) = cuspFunction h f + cuspFunction h g := by
  simp only [cuspFunction, Periodic.cuspFunction]
  ext y
  obtain rfl | hy := eq_or_ne y 0; swap
  ¬∑ simp [hy]
  ¬∑ have : ((f + g) ‚àò ‚ÜëofComplex) ‚àò Periodic.invQParam h = (f ‚àò ‚ÜëofComplex) ‚àò Periodic.invQParam h
        + (g ‚àò ‚ÜëofComplex) ‚àò Periodic.invQParam h := by
      ext y
      simp
    simp only [Pi.add_apply, update_self] at *
    rw [this, Filter.Tendsto.limUnder_eq]
    apply Tendsto.add
    ¬∑ exact tendsto_nhds_limUnder (Exists.intro _ ( periodic_tendto_ndhs_zero hfcts))
    ¬∑ exact tendsto_nhds_limUnder (Exists.intro _ ( periodic_tendto_ndhs_zero hgcts))

lemma qExpansion_sub [Œì.HasDetPlusMinusOne] [DiscreteTopology Œì] (a b : ‚Ñ§)
    (f : ModularForm Œì a) (g : ModularForm Œì b) (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) :
    qExpansion h (f - g) = qExpansion h f - qExpansion h g := by
  ext m
  have := cuspFunction_sub (f := f) (g := g) (h := h)
   (analyticAt_cuspFunction_zero f hh hŒì).continuousAt
   (analyticAt_cuspFunction_zero g hh hŒì).continuousAt
  ¬∑ simp only [qExpansion, this, PowerSeries.coeff_mk, map_sub]
    rw [iteratedDeriv_sub]
    ¬∑ ring
    ¬∑ apply ContDiffOn.contDiffAt  (s := Metric.ball 0 1) ?_ (ball_mem_nhds 0 (by simp))
      refine DifferentiableOn.contDiffOn (fun y hy ‚Ü¶ ?_) (isOpen_ball)
      exact (differentiableAt_cuspFunction f hh hŒì (by simpa using hy)).differentiableWithinAt
    ¬∑ apply ContDiffOn.contDiffAt  (s := Metric.ball 0 1) ?_ (ball_mem_nhds 0 (by simp))
      refine DifferentiableOn.contDiffOn (fun y hy ‚Ü¶ ?_) (isOpen_ball)
      exact (differentiableAt_cuspFunction g hh hŒì (by simpa using hy)).differentiableWithinAt

lemma qExpansion_add [Œì.HasDetPlusMinusOne] [DiscreteTopology Œì] {a b : ‚Ñ§}
    (f : ModularForm Œì a) (g : ModularForm Œì b) (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) :
    qExpansion h (f + g) = qExpansion h f + qExpansion h g := by
  ext m
  have := cuspFunction_add (f := f) (g := g) (h := h)
   (analyticAt_cuspFunction_zero f hh hŒì).continuousAt
   (analyticAt_cuspFunction_zero g hh hŒì).continuousAt
  ¬∑ simp only [qExpansion, this, PowerSeries.coeff_mk, map_add]
    rw [iteratedDeriv_add]
    ¬∑ ring
    ¬∑ apply ContDiffOn.contDiffAt  (s := Metric.ball 0 1) ?_ (ball_mem_nhds 0 (by simp))
      refine DifferentiableOn.contDiffOn (fun y hy ‚Ü¶ ?_) (isOpen_ball)
      exact (differentiableAt_cuspFunction f hh hŒì (by simpa using hy)).differentiableWithinAt
    ¬∑ apply ContDiffOn.contDiffAt  (s := Metric.ball 0 1) ?_ (ball_mem_nhds 0 (by simp))
      refine DifferentiableOn.contDiffOn (fun y hy ‚Ü¶ ?_) (isOpen_ball)
      exact (differentiableAt_cuspFunction g hh hŒì (by simpa using hy)).differentiableWithinAt

lemma IteratedDeriv_smul (a : ‚ÑÇ) (f : ‚ÑÇ ‚Üí ‚ÑÇ) (m : ‚Ñï) :
    iteratedDeriv m (a ‚Ä¢ f) = a ‚Ä¢ iteratedDeriv m f := by
  induction m with
  | zero => simp
  | succ m hm =>
    rw [iteratedDeriv_succ, iteratedDeriv_succ, hm]
    ext x
    rw [@Pi.smul_def]
    exact deriv_const_smul' a ..

lemma qExpansion_smul [Œì.HasDetPlusMinusOne] [DiscreteTopology Œì]
    (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) (a : ‚ÑÇ) (f : ModularForm Œì k) :
    (a ‚Ä¢ qExpansion h f) = (qExpansion h (a ‚Ä¢ f)) := by
  ext m
  simp only [_root_.map_smul, smul_eq_mul]
  simp_rw [qExpansion]
  have : (cuspFunction h (a ‚Ä¢ f)) = a ‚Ä¢ cuspFunction h f := by
    apply  cuspFunction_smul (analyticAt_cuspFunction_zero f hh hŒì).continuousAt
  simp only [PowerSeries.coeff_mk, this]
  conv =>
    enter [2,2]
    rw [IteratedDeriv_smul]
  simp only [Pi.smul_apply, smul_eq_mul]
  ring

@[simp]
lemma qExpansion_zero [Œì.HasDetPlusMinusOne] [DiscreteTopology Œì]
    (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) :
    qExpansion h (0 : ModularForm Œì k) = 0 := by
  simpa using (qExpansion_smul (a := (0 : ‚ÑÇ)) (f := (0 : ModularForm Œì k)) hh hŒì).symm

lemma qExpansion_injective [Œì.HasDetPlusMinusOne] [DiscreteTopology Œì]
    (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) (i : ‚Ñ§) (f : ModularForm Œì i) :
    qExpansion h f = 0 ‚Üî f = 0 := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ ext z
    simp [‚Üê (hasSum_qExpansion f hh hŒì z).tsum_eq, h]
  ¬∑ simp only [h, qExpansion_zero hh hŒì]

def qExpansionAddHom [Œì.HasDetPlusMinusOne] [DiscreteTopology Œì] (hh : 0 < h)
    (hŒì : h ‚àà Œì.strictPeriods) (i : ‚Ñ§) : (ModularForm Œì i) ‚Üí+(PowerSeries ‚ÑÇ) where
  toFun f := qExpansion h f
  map_zero' := by
    rw [ qExpansion_injective hh hŒì]
  map_add' f g := qExpansion_add f g hh hŒì

lemma qExpansion_one [Œì.HasDetPlusMinusOne] : qExpansion h (1 : ModularForm Œì 0) = 1 := by
  ext m
  simp only [qExpansion_coeff]
  by_cases hm : m = 0
  ¬∑ simp only [hm, Nat.factorial_zero, Nat.cast_one, inv_one, cuspFunction, Periodic.cuspFunction,
    one_coe_eq_one, Pi.one_comp, iteratedDeriv_zero, update_self, one_mul, PowerSeries.coeff_one,
    ‚ÜìreduceIte]
    apply Filter.Tendsto.limUnder_eq
    apply tendsto_const_nhds
  ¬∑ simp only [cuspFunction, Periodic.cuspFunction, one_coe_eq_one, Pi.one_comp,
      PowerSeries.coeff_one, hm, ‚ÜìreduceIte, mul_eq_zero, inv_eq_zero, Nat.cast_eq_zero]
    right
    have := iteratedDeriv_const (ùïú := ‚ÑÇ) (F := ‚ÑÇ) (x := 0) (c := 1) (n := m)
    simp only [hm] at this
    convert this
    next z =>
      by_cases hz : z = 0
      ¬∑ rw [hz]
        simp only [update_self]
        apply Filter.Tendsto.limUnder_eq
        apply tendsto_const_nhds
      ¬∑ simp [hz]

open scoped DirectSum in
def qExpansionRingHom [Œì.HasDetPlusMinusOne] [DiscreteTopology Œì] (hh : 0 < h)
    (hŒì : h ‚àà Œì.strictPeriods) : (‚®Å i, ModularForm Œì i) ‚Üí+* (PowerSeries ‚ÑÇ) := by
  apply DirectSum.toSemiring (qExpansionAddHom hh hŒì)
  ¬∑ simp only [qExpansionAddHom, AddMonoidHom.coe_mk, ZeroHom.coe_mk]
    apply qExpansion_one
  ¬∑ intro a b f g
    simp only [qExpansionAddHom, AddMonoidHom.coe_mk, ZeroHom.coe_mk]
    apply qExpansion_mul a b f g hh hŒì

@[simp]
lemma qExpansionRingHom_apply [Œì.HasDetPlusMinusOne] [DiscreteTopology Œì] (hh : 0 < h)
    (hŒì : h ‚àà Œì.strictPeriods) (i : ‚Ñ§) (f : ModularForm Œì i) :
    qExpansionRingHom hh hŒì (DirectSum.of _ i f) = qExpansion h f := by
  simp [qExpansionRingHom, qExpansionAddHom]

lemma qExpansion_of_mul [Œì.HasDetPlusMinusOne] [DiscreteTopology Œì] (hh : 0 < h)
    (hŒì : h ‚àà Œì.strictPeriods) (a b : ‚Ñ§) (f : ModularForm Œì a) (g : ModularForm Œì b) :
    qExpansion h ((((DirectSum.of _ a f)) * (DirectSum.of _ b g)) (a + b)) =
    (qExpansion h f) * (qExpansion h g) := by
  have := (qExpansionRingHom hh hŒì).map_mul (DirectSum.of _ a f) (DirectSum.of _ b g)
  simp only [qExpansionRingHom_apply] at this
  rw [‚Üê this, DirectSum.of_mul_of ]
  simp [qExpansionRingHom_apply]

lemma qExpansion_of_pow [Œì.HasDetPlusMinusOne] [DiscreteTopology Œì] (hh : 0 < h)
    (hŒì : h ‚àà Œì.strictPeriods) (f : ModularForm Œì k) (n : ‚Ñï) :
    qExpansion h ((((DirectSum.of _ k f)) ^ n) (n * k)) = (qExpansion h f) ^ n := by
  have := (qExpansionRingHom hh hŒì).map_pow (DirectSum.of _ k f) n
  simp only [qExpansionRingHom_apply] at this
  rw [‚Üê this, DirectSum.ofPow ]
  simp

end ring

section uniqueness

open Metric Set

open scoped Topology

lemma hasSum_cuspFunction_of_hasSum [Œì.HasDetPlusMinusOne] [DiscreteTopology Œì] {F : Type*}
    [FunLike F ‚Ñç ‚ÑÇ] {k : ‚Ñ§} [ModularFormClass F Œì k]
    (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods) (c : ‚Ñï ‚Üí ‚ÑÇ) (f : F)
    (hf : ‚àÄ (œÑ : ‚Ñç), HasSum (fun m ‚Ü¶ c m ‚Ä¢ ùï¢ h œÑ ^ m) (f œÑ)) {q : ‚ÑÇ} (hq : ‚Äñq‚Äñ < 1)
    (hq1 : q ‚â† 0) : HasSum (fun m ‚Ü¶ c m ‚Ä¢ q ^ m) (cuspFunction h f q) := by
  have h1 := Function.Periodic.im_invQParam_pos_of_norm_lt_one hh hq hq1
  have h2 := hf ‚ü®(Periodic.invQParam h q), h1‚ü©
  have := eq_cuspFunction (h := h) f
    ‚ü®(Periodic.invQParam h q), h1‚ü© hŒì (by grind)
  simp only [smul_eq_mul, ne_eq, coe_mk_subtype] at *
  rw [Function.Periodic.qParam_right_inv (by grind) hq1] at this h2
  simpa [‚Üê this] using h2

theorem cuspfFunction_zero_eq_const_coeff {k : ‚Ñ§} {F : Type*} [FunLike F ‚Ñç ‚ÑÇ]
     {h : ‚Ñù} [Œì.HasDetPlusMinusOne] [DiscreteTopology Œì] (hh : 0 < h) (hŒì : h ‚àà Œì.strictPeriods)
     (c : ‚Ñï ‚Üí ‚ÑÇ) (f : F) [ModularFormClass F Œì k]
     (hf : ‚àÄ (œÑ : ‚Ñç), HasSum (fun m ‚Ü¶ c m ‚Ä¢ ùï¢ h œÑ ^ m) (f œÑ)) : cuspFunction h f 0 = c 0 := by
  rw [cuspFunction, Function.Periodic.cuspFunction_zero_eq_limUnder_nhds_ne]
  apply Filter.Tendsto.limUnder_eq
  have (q : ‚ÑÇ) := hasSum_cuspFunction_of_hasSum hh hŒì c f hf (q := q)
  have htt : Tendsto (fun q ‚Ü¶ ‚àë' m, c m * q ^ m) (ùìù[‚â†] 0) (ùìù (c 0)) := by
    have hD := tendsto_tsum_of_dominated_convergence (ùìï := (ùìù[‚â†] (0 : ‚ÑÇ)))
      (f := fun q : ‚ÑÇ ‚Ü¶ fun m : ‚Ñï ‚Ü¶ c m * q ^ m) (g := fun m : ‚Ñï ‚Ü¶ c m * 0 ^ m)
      (bound := fun m ‚Ü¶ ‚Äñc m‚Äñ * (1 / 2 ) ^ m ) ?_ ?_ ?_
    ¬∑ convert hD
      rw [Summable.tsum_eq_zero_add (by simpa [‚Üê summable_nat_add_iff 1] using summable_zero)]
      simp
    ¬∑ simpa using (this (1/2) (by norm_num)
        (by apply one_div_ne_zero; exact Ne.symm (NeZero.ne' 2))).summable.norm
    ¬∑ exact fun k ‚Ü¶ Tendsto.const_mul (c k)
        (Tendsto.mono_left (Continuous.tendsto (continuous_pow k) 0) nhdsWithin_le_nhds)
    ¬∑ rw [eventually_iff_exists_mem]
      use {z | (z : ‚ÑÇ) ‚â† 0 ‚àß ‚Äñz‚Äñ < 1 / 2}
      constructor
      ¬∑ rw [@mem_nhdsWithin_iff]
        refine ‚ü®1 / 2, by norm_num, fun y hy ‚Ü¶ ?_‚ü©
        simp only [Subgroup.mem_strictPeriods_iff, Matrix.GeneralLinearGroup.upperRightHom_apply,
          smul_eq_mul, ne_eq, one_div, mem_inter_iff, mem_ball, dist_zero_right, mem_compl_iff,
          mem_singleton_iff, mem_setOf_eq] at *
        refine ‚ü®hy.2, hy.1‚ü©
      ¬∑ intro y hy k
        simp only [norm_mul, norm_pow]
        gcongr
        simpa using hy.2.le
  apply htt.congr'
  rw [@eventuallyEq_nhdsWithin_iff, eventually_nhds_iff_ball]
  refine ‚ü®1, by simpa using fun y hy hy0 ‚Ü¶ (this y hy hy0).tsum_eq‚ü©

lemma modfom_q_exp_cuspfunc [Œì.HasDetPlusMinusOne] [DiscreteTopology Œì] (hh : 0 < h)
    (hŒì : h ‚àà Œì.strictPeriods) (c : ‚Ñï ‚Üí ‚ÑÇ) (f : F) [ModularFormClass F Œì k]
    (hf : ‚àÄ œÑ : ‚Ñç, HasSum (fun m : ‚Ñï ‚Ü¶ (c m) ‚Ä¢ ùï¢ h œÑ ^ m) (f œÑ)) : ‚àÄ q : ‚ÑÇ, ‚Äñq‚Äñ < 1 ‚Üí
    HasSum (fun m : ‚Ñï ‚Ü¶ c m ‚Ä¢ q ^ m) (cuspFunction h f q) := by
  intro q hq
  by_cases hq1 : q ‚â† 0
  ¬∑ exact hasSum_cuspFunction_of_hasSum hh hŒì c f hf hq hq1
  ¬∑ simp only [ne_eq, Decidable.not_not] at hq1
    simp_rw [hq1, cuspfFunction_zero_eq_const_coeff hh hŒì c f hf, smul_eq_mul]
    rw [Summable.hasSum_iff (by simpa [‚Üê summable_nat_add_iff 1] using summable_zero),
      Summable.tsum_eq_zero_add (by simpa [‚Üê summable_nat_add_iff 1] using summable_zero)]
    simp

private lemma qParam_onto_annulus (r h : ‚Ñù) (hr : 0 < r) (hr2 : r < 1) (hh : 0 < h) :
    ‚àÉ (z : ‚Ñç), ‚Äñùï¢ h z‚Äñ = r := by
  use ‚ü®Periodic.invQParam h r, ?_‚ü©
  ¬∑ have hq := Function.Periodic.qParam_right_inv (h := h) (q := r) (by grind) (by aesop)
    simp [UpperHalfPlane.coe, hq, hr.le]
  ¬∑ rw [Function.Periodic.im_invQParam, norm_real, Real.norm_eq_abs, Real.log_abs, mul_pos_iff]
    right
    refine ‚ü®div_neg_of_neg_of_pos (by simp [hh]) (Real.two_pi_pos), (Real.log_neg_iff hr).mpr hr2‚ü©

lemma qExpansion_coeffs_unique (c : ‚Ñï ‚Üí ‚ÑÇ) [Œì.HasDetPlusMinusOne] [DiscreteTopology Œì] (hh : 0 < h)
    (hŒì : h ‚àà Œì.strictPeriods) (f : ModularForm Œì k)
    (hf : ‚àÄ œÑ : ‚Ñç, HasSum (fun m : ‚Ñï ‚Ü¶ (c m) ‚Ä¢ ùï¢ h œÑ ^ m) (f œÑ)) :
    c = (fun m ‚Ü¶ (qExpansion h f).coeff m) := by
  ext m
  have h0 := hasFPowerSeries_cuspFunction (h := h) f hh hŒì
  let qExpansion2 : PowerSeries ‚ÑÇ := .mk fun m ‚Ü¶ c m
  let qq : FormalMultilinearSeries ‚ÑÇ ‚ÑÇ ‚ÑÇ :=
    fun m ‚Ü¶ (qExpansion2).coeff m ‚Ä¢ ContinuousMultilinearMap.mkPiAlgebraFin ‚ÑÇ m _
  have hqq2 : ‚àÄ m , ‚Äñqq m‚Äñ = ‚Äñ(qExpansion2).coeff m‚Äñ := by
    intro m
    unfold qq
    rw [‚Üê (ContinuousMultilinearMap.piFieldEquiv ‚ÑÇ (Fin m) ‚ÑÇ).symm.norm_map]
    simp only [_root_.map_smul, smul_eq_mul, norm_mul,
      LinearIsometryEquiv.norm_map, ContinuousMultilinearMap.norm_mkPiAlgebraFin, mul_one]
  have H2 : HasFPowerSeriesOnBall (cuspFunction h f) qq 0 1 := by
    have H21 : 1 ‚â§ qq.radius := by
        refine le_of_forall_lt_imp_le_of_dense fun r hr ‚Ü¶ ?_
        lift r to NNReal using hr.ne_top
        apply FormalMultilinearSeries.le_radius_of_summable
        simp only [hqq2, PowerSeries.coeff_mk, qExpansion2]
        by_cases hr0 : r = 0
        ¬∑ rw [hr0, ‚Üê summable_nat_add_iff 1]
          simpa using summable_zero
        ¬∑ obtain ‚ü®z, hz‚ü© := qParam_onto_annulus r h ((by simp [pos_iff_ne_zero.mpr hr0] ))
            (by simpa using hr) hh
          simpa [NNReal.coe_pow, ‚Üê hz] using (summable_norm_iff.mpr (hf z).summable)
    refine ‚ü®H21 , zero_lt_one, ?_‚ü©
    intro y hy
    simp only [EMetric.mem_ball, edist_zero_right, enorm_eq_nnnorm, ENNReal.coe_lt_one_iff,
      ‚Üê NNReal.coe_lt_one, coe_nnnorm, zero_add] at hy ‚ä¢
    apply (modfom_q_exp_cuspfunc hh hŒì c f hf y hy).congr
    simp [smul_eq_mul, PowerSeries.coeff_mk, qq, qExpansion2]
  have h3 : HasFPowerSeriesAt (cuspFunction h f) qq 0 := by
    use 1
  have h4 : HasFPowerSeriesAt (cuspFunction h f) (qExpansionFormalMultilinearSeries h f) 0 := by
    use 1
  have := FormalMultilinearSeries.ext_iff.mp (HasFPowerSeriesAt.eq_formalMultilinearSeries h3 h4) m
  simp only [PowerSeries.coeff_mk, qExpansionFormalMultilinearSeries, qq, qExpansion2] at this
  have htv : (c m ‚Ä¢ ContinuousMultilinearMap.mkPiAlgebraFin ‚ÑÇ m ‚ÑÇ).toFun = ((PowerSeries.coeff m)
    (qExpansion h f) ‚Ä¢ ContinuousMultilinearMap.mkPiAlgebraFin ‚ÑÇ m ‚ÑÇ).toFun := by
    rw [this]
  simpa [Pi.natCast_def, qExpansion2, qq] using (congrFun htv m)


end uniqueness
