/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Analysis.Complex.TaylorSeries
import Mathlib.Analysis.Complex.UpperHalfPlane.Exp
import Mathlib.NumberTheory.ModularForms.Basic
import Mathlib.NumberTheory.ModularForms.Identities
import Mathlib.RingTheory.PowerSeries.Basic

/-!
# q-expansions of modular forms

We show that a modular form of level `Î“(n)` can be written as `Ï„ â†¦ F (ð•¢ n Ï„)` where `F` is
analytic on the open unit disc, and `ð•¢ n` is the parameter `Ï„ â†¦ exp (2 * I * Ï€ * Ï„ / n)`. As an
application, we show that cusp forms decay exponentially to 0 as `im Ï„ â†’ âˆž`.

We also define the `q`-expansion of a modular form, either as a power series or as a
`FormalMultilinearSeries`, and show that it converges to `f` on the upper half plane.

## Main definitions and results

* `SlashInvariantFormClass.cuspFunction`: for a level `n` slash-invariant form, this is the function
  `F` such that `f Ï„ = F (exp (2 * Ï€ * I * Ï„ / n))`, extended by a choice of limit at `0`.
* `ModularFormClass.differentiableAt_cuspFunction`: when `f` is a modular form, its `cuspFunction`
  is differentiable on the open unit disc (including at `0`).
* `ModularFormClass.qExpansion`: the `q`-expansion of a modular form (defined as the Taylor series
  of its `cuspFunction`), bundled as a `PowerSeries`.
* `ModularFormClass.hasSum_qExpansion`: the `q`-expansion evaluated at `ð•¢ n Ï„` sums to `f Ï„`, for
  `Ï„` in the upper half plane.

## TO DO:

* generalise to handle arbitrary finite-index subgroups (not just `Î“(n)` for some `n`)
* define the `q`-expansion map on modular form spaces as a linear map (or even a ring hom from
  the graded ring of all modular forms?)
-/

open ModularForm Complex Filter UpperHalfPlane Function Matrix.SpecialLinearGroup

open scoped Real MatrixGroups CongruenceSubgroup

noncomputable section

variable {k : â„¤} {F : Type*} [FunLike F â„ â„‚] {Î“ : Subgroup (GL (Fin 2) â„)}
    {h : â„} (f : F)

local notation "Iâˆž" => comap Complex.im atTop
local notation "ð•¢" => Periodic.qParam

namespace SlashInvariantFormClass

theorem periodic_comp_ofComplex [SlashInvariantFormClass F Î“ k] (hÎ“ : h âˆˆ Î“.strictPeriods) :
    Periodic (f âˆ˜ ofComplex) h := by
  intro w
  by_cases hw : 0 < im w
  Â· have : 0 < im (w + h) := by simp [hw]
    simp only [comp_apply, ofComplex_apply_of_im_pos this, ofComplex_apply_of_im_pos hw]
    convert SlashInvariantForm.vAdd_apply_of_mem_strictPeriods f âŸ¨w, hwâŸ© hÎ“ using 2
    ext
    simp [add_comm]
  Â· have : im (w + h) â‰¤ 0 := by simpa using hw
    simp only [comp_apply, ofComplex_apply_of_im_nonpos this,
      ofComplex_apply_of_im_nonpos (not_lt.mp hw)]

variable (h) in
/--
The analytic function `F` such that `f Ï„ = F (exp (2 * Ï€ * I * Ï„ / n))`, extended by a choice of
limit at `0`.
-/
def cuspFunction : â„‚ â†’ â„‚ := Function.Periodic.cuspFunction h (f âˆ˜ ofComplex)

theorem eq_cuspFunction [SlashInvariantFormClass F Î“ k] (Ï„ : â„) (hÎ“ : h âˆˆ Î“.strictPeriods)
    (hh : h â‰  0) : cuspFunction h f (ð•¢ h Ï„) = f Ï„ := by
  simpa only [comp_apply, ofComplex_apply]
    using (periodic_comp_ofComplex f hÎ“).eq_cuspFunction hh Ï„

end SlashInvariantFormClass

open SlashInvariantFormClass

namespace ModularFormClass

theorem differentiableAt_comp_ofComplex [ModularFormClass F Î“ k]
      {z : â„‚} (hz : 0 < im z) :
    DifferentiableAt â„‚ (f âˆ˜ ofComplex) z :=
  mdifferentiableAt_iff_differentiableAt.mp ((holo f _).comp z (mdifferentiableAt_ofComplex hz))

theorem bounded_at_infty_comp_ofComplex [ModularFormClass F Î“ k] (hi : IsCusp OnePoint.infty Î“) :
    BoundedAtFilter Iâˆž (f âˆ˜ ofComplex) :=
  (OnePoint.isBoundedAt_infty_iff.mp (bdd_at_cusps f hi)).comp_tendsto tendsto_comap_im_ofComplex

theorem differentiableAt_cuspFunction [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) {q : â„‚} (hq : â€–qâ€– < 1) :
    DifferentiableAt â„‚ (cuspFunction h f) q := by
  have hi : IsCusp OnePoint.infty Î“ := by
    rw [Subgroup.strictPeriods_eq_zmultiples_strictWidthInfty] at hÎ“
    refine Î“.strictWidthInfty_pos_iff.mp <| Î“.strictWidthInfty_nonneg.lt_of_ne' fun h0 â†¦ hh.ne' ?_
    simpa only [h0, AddSubgroup.zmultiples_zero_eq_bot, AddSubgroup.mem_bot] using hÎ“
  rcases eq_or_ne q 0 with rfl | hq'
  Â· exact (periodic_comp_ofComplex f hÎ“).differentiableAt_cuspFunction_zero hh
      (eventually_of_mem (preimage_mem_comap (Ioi_mem_atTop 0))
        (fun _ â†¦ differentiableAt_comp_ofComplex f))
      (bounded_at_infty_comp_ofComplex f hi)
  Â· exact Periodic.qParam_right_inv hh.ne' hq' â–¸
      (periodic_comp_ofComplex f hÎ“).differentiableAt_cuspFunction hh.ne'
        <| differentiableAt_comp_ofComplex _ <| Periodic.im_invQParam_pos_of_norm_lt_one hh hq hq'

lemma analyticAt_cuspFunction_zero [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) :
    AnalyticAt â„‚ (cuspFunction h f) 0 :=
  DifferentiableOn.analyticAt
    (fun q hq â†¦ (differentiableAt_cuspFunction _ hh hÎ“ hq).differentiableWithinAt)
    (by simpa only [ball_zero_eq] using Metric.ball_mem_nhds (0 : â„‚) zero_lt_one)

variable (h) in
/-- The `q`-expansion of a level `n` modular form, bundled as a `PowerSeries`. -/
def qExpansion : PowerSeries â„‚ :=
  .mk fun m â†¦ (â†‘m.factorial)â»Â¹ * iteratedDeriv m (cuspFunction h f) 0

lemma qExpansion_coeff (m : â„•) :
    (qExpansion h f).coeff m = (â†‘m.factorial)â»Â¹ * iteratedDeriv m (cuspFunction h f) 0 := by
  simp [qExpansion]

lemma hasSum_qExpansion_of_abs_lt [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) {q : â„‚} (hq : â€–qâ€– < 1) :
    HasSum (fun m : â„• â†¦ (qExpansion h f).coeff m â€¢ q ^ m) (cuspFunction h f q) := by
  simp only [qExpansion_coeff]
  have hdiff : DifferentiableOn â„‚ (cuspFunction h f) (Metric.ball 0 1) := by
    refine fun z hz â†¦ (differentiableAt_cuspFunction f hh hÎ“ ?_).differentiableWithinAt
    simpa using hz
  have qmem : q âˆˆ Metric.ball 0 1 := by simpa using hq
  convert hasSum_taylorSeries_on_ball hdiff qmem using 2 with m
  rw [sub_zero, smul_eq_mul, smul_eq_mul, mul_right_comm, smul_eq_mul, mul_assoc]

lemma hasSum_qExpansion [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) (Ï„ : â„) :
    HasSum (fun m : â„• â†¦ (qExpansion h f).coeff m â€¢ ð•¢ h Ï„ ^ m) (f Ï„) := by
  have : â€–ð•¢ h Ï„â€– < 1 := by simp [Periodic.qParam, Complex.norm_exp, neg_div]; positivity
  simpa only [eq_cuspFunction f _ hÎ“ hh.ne'] using hasSum_qExpansion_of_abs_lt f hh hÎ“ this

variable (h) in
/--
The `q`-expansion of a level `n` modular form, bundled as a `FormalMultilinearSeries`.

TODO: Maybe get rid of this and instead define a general API for converting `PowerSeries` to
`FormalMultilinearSeries`.
-/
def qExpansionFormalMultilinearSeries : FormalMultilinearSeries â„‚ â„‚ â„‚ :=
  fun m â†¦ (qExpansion h f).coeff m â€¢ ContinuousMultilinearMap.mkPiAlgebraFin â„‚ m _

lemma qExpansionFormalMultilinearSeries_apply_norm (m : â„•) :
    â€–qExpansionFormalMultilinearSeries h f mâ€– = â€–(qExpansion h f).coeff mâ€– := by
  rw [qExpansionFormalMultilinearSeries,
    â† (ContinuousMultilinearMap.piFieldEquiv â„‚ (Fin m) â„‚).symm.norm_map]
  simp

lemma qExpansionFormalMultilinearSeries_radius [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) :
    1 â‰¤ (qExpansionFormalMultilinearSeries h f).radius := by
  refine le_of_forall_lt_imp_le_of_dense fun r hr â†¦ ?_
  lift r to NNReal using hr.ne_top
  apply FormalMultilinearSeries.le_radius_of_summable
  simp only [qExpansionFormalMultilinearSeries_apply_norm]
  rw [â† r.abs_eq]
  simp_rw [â† Real.norm_eq_abs, â† Complex.norm_real, â† norm_pow, â† norm_mul]
  exact (hasSum_qExpansion_of_abs_lt f hh hÎ“ (q := r) (by simpa using hr)).summable.norm

/-- The `q`-expansion of `f` is an `FPowerSeries` representing `cuspFunction n f`. -/
lemma hasFPowerSeries_cuspFunction [ModularFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) :
    HasFPowerSeriesOnBall (cuspFunction h f) (qExpansionFormalMultilinearSeries h f) 0 1 := by
  refine âŸ¨qExpansionFormalMultilinearSeries_radius f hh hÎ“, zero_lt_one, fun hy â†¦ ?_âŸ©
  rw [EMetric.mem_ball, edist_zero_right, enorm_eq_nnnorm, ENNReal.coe_lt_one_iff,
    â† NNReal.coe_lt_one, coe_nnnorm] at hy
  simpa [qExpansionFormalMultilinearSeries] using hasSum_qExpansion_of_abs_lt f hh hÎ“ hy

end ModularFormClass

open ModularFormClass

namespace CuspFormClass

theorem zero_at_infty_comp_ofComplex [CuspFormClass F Î“ k] (hi : IsCusp OnePoint.infty Î“) :
    ZeroAtFilter Iâˆž (f âˆ˜ ofComplex) :=
  (OnePoint.isZeroAt_infty_iff.mp (zero_at_cusps f hi)).comp tendsto_comap_im_ofComplex

theorem cuspFunction_apply_zero [CuspFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) :
    cuspFunction h f 0 = 0 :=
  have hi : IsCusp OnePoint.infty Î“ := by
    rw [Subgroup.strictPeriods_eq_zmultiples_strictWidthInfty] at hÎ“
    refine Î“.strictWidthInfty_pos_iff.mp <| Î“.strictWidthInfty_nonneg.lt_of_ne' fun h0 â†¦ hh.ne' ?_
    simpa only [h0, AddSubgroup.zmultiples_zero_eq_bot, AddSubgroup.mem_bot] using hÎ“
  Periodic.cuspFunction_zero_of_zero_at_inf hh (zero_at_infty_comp_ofComplex f hi)

theorem exp_decay_atImInfty [CuspFormClass F Î“ k] [Î“.HasDetPlusMinusOne]
    [DiscreteTopology Î“] (hh : 0 < h) (hÎ“ : h âˆˆ Î“.strictPeriods) :
    f =O[atImInfty] fun Ï„ â†¦ Real.exp (-2 * Ï€ * Ï„.im / h) := by
  have hi : IsCusp OnePoint.infty Î“ := by
    rw [Subgroup.strictPeriods_eq_zmultiples_strictWidthInfty] at hÎ“
    refine Î“.strictWidthInfty_pos_iff.mp <| Î“.strictWidthInfty_nonneg.lt_of_ne' fun h0 â†¦ hh.ne' ?_
    simpa only [h0, AddSubgroup.zmultiples_zero_eq_bot, AddSubgroup.mem_bot] using hÎ“
  simpa only [neg_mul, comp_def, ofComplex_apply, coe_im] using
    ((periodic_comp_ofComplex f hÎ“).exp_decay_of_zero_at_inf hh
      (eventually_of_mem (preimage_mem_comap (Ioi_mem_atTop 0))
        fun _ â†¦ differentiableAt_comp_ofComplex f)
      (zero_at_infty_comp_ofComplex f hi)).comp_tendsto tendsto_coe_atImInfty

end CuspFormClass
