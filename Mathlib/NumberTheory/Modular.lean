/-
Copyright (c) 2021 Alex Kontorovich and Heather Macbeth and Marc Masdeu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alex Kontorovich, Heather Macbeth, Marc Masdeu
-/
import Mathlib.Analysis.Complex.UpperHalfPlane.MoebiusAction
import Mathlib.LinearAlgebra.GeneralLinearGroup
import Mathlib.LinearAlgebra.Matrix.GeneralLinearGroup.Basic
import Mathlib.Topology.Instances.Matrix
import Mathlib.Topology.Algebra.Module.FiniteDimension
import Mathlib.Topology.Instances.ZMultiples

/-!
# The action of the modular group SL(2, â„¤) on the upper half-plane

We define the action of `SL(2,â„¤)` on `â„` (via restriction of the `SL(2,â„)` action in
`Analysis.Complex.UpperHalfPlane`). We then define the standard fundamental domain
(`ModularGroup.fd`, `ğ’Ÿ`) for this action and show
(`ModularGroup.exists_smul_mem_fd`) that any point in `â„` can be
moved inside `ğ’Ÿ`.

## Main definitions

The standard (closed) fundamental domain of the action of `SL(2,â„¤)` on `â„`, denoted `ğ’Ÿ`:
`fd := {z | 1 â‰¤ (z : â„‚).normSq âˆ§ |z.re| â‰¤ (1 : â„) / 2}`

The standard open fundamental domain of the action of `SL(2,â„¤)` on `â„`, denoted `ğ’Ÿáµ’`:
`fdo := {z | 1 < (z : â„‚).normSq âˆ§ |z.re| < (1 : â„) / 2}`

These notations are localized in the `Modular` locale and can be enabled via `open scoped Modular`.

## Main results

Any `z : â„` can be moved to `ğ’Ÿ` by an element of `SL(2,â„¤)`:
`exists_smul_mem_fd (z : â„) : âˆƒ g : SL(2,â„¤), g â€¢ z âˆˆ ğ’Ÿ`

If both `z` and `Î³ â€¢ z` are in the open domain `ğ’Ÿáµ’` then `z = Î³ â€¢ z`:
`eq_smul_self_of_mem_fdo_mem_fdo {z : â„} {g : SL(2,â„¤)} (hz : z âˆˆ ğ’Ÿáµ’) (hg : g â€¢ z âˆˆ ğ’Ÿáµ’) : z = g â€¢ z`

# Discussion

Standard proofs make use of the identity

`g â€¢ z = a / c - 1 / (c (cz + d))`

for `g = [[a, b], [c, d]]` in `SL(2)`, but this requires separate handling of whether `c = 0`.
Instead, our proof makes use of the following perhaps novel identity (see
`ModularGroup.smul_eq_lcRow0_add`):

`g â€¢ z = (a c + b d) / (c^2 + d^2) + (d z - c) / ((c^2 + d^2) (c z + d))`

where there is no issue of division by zero.

Another feature is that we delay until the very end the consideration of special matrices
`T=[[1,1],[0,1]]` (see `ModularGroup.T`) and `S=[[0,-1],[1,0]]` (see `ModularGroup.S`), by
instead using abstract theory on the properness of certain maps (phrased in terms of the filters
`Filter.cocompact`, `Filter.cofinite`, etc) to deduce existence theorems, first to prove the
existence of `g` maximizing `(gâ€¢z).im` (see `ModularGroup.exists_max_im`), and then among
those, to minimize `|(gâ€¢z).re|` (see `ModularGroup.exists_row_one_eq_and_min_re`).
-/


open Complex hiding abs_two

open Matrix hiding mul_smul

open Matrix.SpecialLinearGroup UpperHalfPlane ModularGroup Topology

noncomputable section

open scoped ComplexConjugate MatrixGroups

namespace ModularGroup

variable {g : SL(2, â„¤)} (z : â„)

section BottomRow

/-- The two numbers `c`, `d` in the "bottom_row" of `g=[[*,*],[c,d]]` in `SL(2, â„¤)` are coprime. -/
theorem bottom_row_coprime {R : Type*} [CommRing R] (g : SL(2, R)) :
    IsCoprime ((â†‘g : Matrix (Fin 2) (Fin 2) R) 1 0) ((â†‘g : Matrix (Fin 2) (Fin 2) R) 1 1) := by
  use -(â†‘g : Matrix (Fin 2) (Fin 2) R) 0 1, (â†‘g : Matrix (Fin 2) (Fin 2) R) 0 0
  rw [add_comm, neg_mul, â† sub_eq_add_neg, â† det_fin_two]
  exact g.det_coe

/-- Every pair `![c, d]` of coprime integers is the "bottom_row" of some element `g=[[*,*],[c,d]]`
of `SL(2,â„¤)`. -/
theorem bottom_row_surj {R : Type*} [CommRing R] :
    Set.SurjOn (fun g : SL(2, R) => (â†‘g : Matrix (Fin 2) (Fin 2) R) 1) Set.univ
      {cd | IsCoprime (cd 0) (cd 1)} := by
  rintro cd âŸ¨bâ‚€, a, gcd_eqnâŸ©
  let A := of ![![a, -bâ‚€], cd]
  have det_A_1 : det A = 1 := by
    convert gcd_eqn
    rw [det_fin_two]
    simp [A, (by ring : a * cd 1 + bâ‚€ * cd 0 = bâ‚€ * cd 0 + a * cd 1)]
  refine âŸ¨âŸ¨A, det_A_1âŸ©, Set.mem_univ _, ?_âŸ©
  ext; simp [A]

end BottomRow

section TendstoLemmas

open Filter ContinuousLinearMap

attribute [local simp] ContinuousLinearMap.coe_smul

/-- The function `(c,d) â†’ |cz+d|^2` is proper, that is, preimages of bounded-above sets are finite.
-/
theorem tendsto_normSq_coprime_pair :
    Filter.Tendsto (fun p : Fin 2 â†’ â„¤ => normSq ((p 0 : â„‚) * z + p 1)) cofinite atTop := by
  -- using this instance rather than the automatic `Function.module` makes unification issues in
  -- `LinearEquiv.isClosedEmbedding_of_injective` less bad later in the proof.
  letI : Module â„ (Fin 2 â†’ â„) := NormedSpace.toModule
  let Ï€â‚€ : (Fin 2 â†’ â„) â†’â‚—[â„] â„ := LinearMap.proj 0
  let Ï€â‚ : (Fin 2 â†’ â„) â†’â‚—[â„] â„ := LinearMap.proj 1
  let f : (Fin 2 â†’ â„) â†’â‚—[â„] â„‚ := Ï€â‚€.smulRight (z : â„‚) + Ï€â‚.smulRight 1
  have f_def : â‡‘f = fun p : Fin 2 â†’ â„ => (p 0 : â„‚) * â†‘z + p 1 := by
    ext1
    dsimp only [Ï€â‚€, Ï€â‚, f, LinearMap.coe_proj, real_smul, LinearMap.coe_smulRight,
      LinearMap.add_apply]
    rw [mul_one]
  have :
    (fun p : Fin 2 â†’ â„¤ => normSq ((p 0 : â„‚) * â†‘z + â†‘(p 1))) =
      normSq âˆ˜ f âˆ˜ fun p : Fin 2 â†’ â„¤ => ((â†‘) : â„¤ â†’ â„) âˆ˜ p := by
    ext1
    rw [f_def]
    dsimp only [Function.comp_def]
    rw [ofReal_intCast, ofReal_intCast]
  rw [this]
  have hf : LinearMap.ker f = âŠ¥ := by
    let g : â„‚ â†’â‚—[â„] Fin 2 â†’ â„ :=
      LinearMap.pi ![imLm, imLm.comp ((z : â„‚) â€¢ ((conjAe : â„‚ â†’â‚[â„] â„‚) : â„‚ â†’â‚—[â„] â„‚))]
    suffices ((z : â„‚).imâ»Â¹ â€¢ g).comp f = LinearMap.id by exact LinearMap.ker_eq_bot_of_inverse this
    apply LinearMap.ext
    intro c
    have hz : (z : â„‚).im â‰  0 := z.2.ne'
    rw [LinearMap.comp_apply, LinearMap.smul_apply, LinearMap.id_apply]
    ext i
    dsimp only [Pi.smul_apply, LinearMap.pi_apply, smul_eq_mul]
    fin_cases i
    Â· show (z : â„‚).imâ»Â¹ * (f c).im = c 0
      rw [f_def, add_im, im_ofReal_mul, ofReal_im, add_zero, mul_left_comm, inv_mul_cancelâ‚€ hz,
        mul_one]
    Â· show (z : â„‚).imâ»Â¹ * ((z : â„‚) * conj (f c)).im = c 1
      rw [f_def, RingHom.map_add, RingHom.map_mul, mul_add, mul_left_comm, mul_conj, conj_ofReal,
        conj_ofReal, â† ofReal_mul, add_im, ofReal_im, zero_add, inv_mul_eq_iff_eq_mulâ‚€ hz]
      simp only [ofReal_im, ofReal_re, mul_im, zero_add, mul_zero]
  have hf' : IsClosedEmbedding f := f.isClosedEmbedding_of_injective hf
  have hâ‚‚ : Tendsto (fun p : Fin 2 â†’ â„¤ => ((â†‘) : â„¤ â†’ â„) âˆ˜ p) cofinite (cocompact _) := by
    convert Tendsto.pi_map_coprodáµ¢ fun _ => Int.tendsto_coe_cofinite
    Â· rw [coprodáµ¢_cofinite]
    Â· rw [coprodáµ¢_cocompact]
  exact tendsto_normSq_cocompact_atTop.comp (hf'.tendsto_cocompact.comp hâ‚‚)

/-- Given `coprime_pair` `p=(c,d)`, the matrix `[[a,b],[*,*]]` is sent to `a*c+b*d`.
  This is the linear map version of this operation.
-/
def lcRow0 (p : Fin 2 â†’ â„¤) : Matrix (Fin 2) (Fin 2) â„ â†’â‚—[â„] â„ :=
  ((p 0 : â„) â€¢ LinearMap.proj (0 : Fin 2) +
      (p 1 : â„) â€¢ LinearMap.proj (1 : Fin 2) : (Fin 2 â†’ â„) â†’â‚—[â„] â„).comp
    (LinearMap.proj 0)

@[simp]
theorem lcRow0_apply (p : Fin 2 â†’ â„¤) (g : Matrix (Fin 2) (Fin 2) â„) :
    lcRow0 p g = p 0 * g 0 0 + p 1 * g 0 1 :=
  rfl

/-- Linear map sending the matrix [a, b; c, d] to the matrix [acâ‚€ + bdâ‚€, - adâ‚€ + bcâ‚€; c, d], for
some fixed `(câ‚€, dâ‚€)`. -/
@[simps!]
def lcRow0Extend {cd : Fin 2 â†’ â„¤} (hcd : IsCoprime (cd 0) (cd 1)) :
    Matrix (Fin 2) (Fin 2) â„ â‰ƒâ‚—[â„] Matrix (Fin 2) (Fin 2) â„ :=
  LinearEquiv.piCongrRight
    ![by
      refine
        LinearMap.GeneralLinearGroup.generalLinearEquiv â„ (Fin 2 â†’ â„)
          (GeneralLinearGroup.toLin (planeConformalMatrix (cd 0 : â„) (-(cd 1 : â„)) ?_))
      norm_cast
      rw [neg_sq]
      exact hcd.sq_add_sq_ne_zero, LinearEquiv.refl â„ (Fin 2 â†’ â„)]

/-- The map `lcRow0` is proper, that is, preimages of cocompact sets are finite in
`[[* , *], [c, d]]`. -/
theorem tendsto_lcRow0 {cd : Fin 2 â†’ â„¤} (hcd : IsCoprime (cd 0) (cd 1)) :
    Tendsto (fun g : { g : SL(2, â„¤) // g 1 = cd } => lcRow0 cd â†‘(â†‘g : SL(2, â„))) cofinite
      (cocompact â„) := by
  let mB : â„ â†’ Matrix (Fin 2) (Fin 2) â„ := fun t => of ![![t, (-(1 : â„¤) : â„)], (â†‘) âˆ˜ cd]
  have hmB : Continuous mB := by
    refine continuous_matrix ?_
    simp only [mB, Fin.forall_fin_two, continuous_const, continuous_id', of_apply, cons_val_zero,
      cons_val_one, and_self_iff]
  refine Filter.Tendsto.of_tendsto_comp ?_ (comap_cocompact_le hmB)
  let fâ‚ : SL(2, â„¤) â†’ Matrix (Fin 2) (Fin 2) â„ := fun g =>
    Matrix.map (â†‘g : Matrix _ _ â„¤) ((â†‘) : â„¤ â†’ â„)
  have cocompact_â„_to_cofinite_â„¤_matrix :
    Tendsto (fun m : Matrix (Fin 2) (Fin 2) â„¤ => Matrix.map m ((â†‘) : â„¤ â†’ â„)) cofinite
      (cocompact _) := by
    simpa only [coprodáµ¢_cofinite, coprodáµ¢_cocompact] using
      Tendsto.pi_map_coprodáµ¢ fun _ : Fin 2 =>
        Tendsto.pi_map_coprodáµ¢ fun _ : Fin 2 => Int.tendsto_coe_cofinite
  have hfâ‚ : Tendsto fâ‚ cofinite (cocompact _) :=
    cocompact_â„_to_cofinite_â„¤_matrix.comp Subtype.coe_injective.tendsto_cofinite
  have hfâ‚‚ : IsClosedEmbedding (lcRow0Extend hcd) :=
    (lcRow0Extend hcd).toContinuousLinearEquiv.toHomeomorph.isClosedEmbedding
  convert hfâ‚‚.tendsto_cocompact.comp (hfâ‚.comp Subtype.coe_injective.tendsto_cofinite) using 1
  ext âŸ¨g, rflâŸ© i j : 3
  fin_cases i <;> [fin_cases j; skip]
  -- the following are proved by `simp`, but it is replaced by `simp only` to avoid timeouts.
  Â· simp only [Fin.isValue, Int.cast_one, map_apply_coe, RingHom.mapMatrix_apply,
      Int.coe_castRingHom, lcRow0_apply, map_apply, Fin.zero_eta, id_eq, Function.comp_apply,
      of_apply, cons_val', cons_val_zero, empty_val', cons_val_fin_one, lcRow0Extend_apply,
      LinearMap.GeneralLinearGroup.coeFn_generalLinearEquiv, GeneralLinearGroup.coe_toLin,
      val_planeConformalMatrix, neg_neg, mulVecLin_apply, mulVec, dotProduct, Fin.sum_univ_two,
      cons_val_one, head_cons, mB, fâ‚]
  Â· convert congr_arg (fun n : â„¤ => (-n : â„)) g.det_coe.symm using 1
    simp only [Fin.zero_eta, id_eq, Function.comp_apply, lcRow0Extend_apply, cons_val_zero,
      LinearMap.GeneralLinearGroup.coeFn_generalLinearEquiv, GeneralLinearGroup.coe_toLin,
      mulVecLin_apply, mulVec, dotProduct, det_fin_two, fâ‚]
    simp only [Fin.isValue, Fin.mk_one, val_planeConformalMatrix, neg_neg, of_apply, cons_val',
      empty_val', cons_val_fin_one, cons_val_one, head_fin_const, map_apply, Fin.sum_univ_two,
      cons_val_zero, neg_mul, head_cons, Int.cast_sub, Int.cast_mul, neg_sub]
    ring
  Â· rfl

/-- This replaces `(gâ€¢z).re = a/c + *` in the standard theory with the following novel identity:
  `g â€¢ z = (a c + b d) / (c^2 + d^2) + (d z - c) / ((c^2 + d^2) (c z + d))`
  which does not need to be decomposed depending on whether `c = 0`. -/
theorem smul_eq_lcRow0_add {p : Fin 2 â†’ â„¤} (hp : IsCoprime (p 0) (p 1)) (hg : g 1 = p) :
    â†‘(g â€¢ z) =
      (lcRow0 p â†‘(g : SL(2, â„)) : â„‚) / ((p 0 : â„‚) ^ 2 + (p 1 : â„‚) ^ 2) +
        ((p 1 : â„‚) * z - p 0) / (((p 0 : â„‚) ^ 2 + (p 1 : â„‚) ^ 2) * (p 0 * z + p 1)) := by
  have nonZ1 : (p 0 : â„‚) ^ 2 + (p 1 : â„‚) ^ 2 â‰  0 := mod_cast hp.sq_add_sq_ne_zero
  have : ((â†‘) : â„¤ â†’ â„) âˆ˜ p â‰  0 := fun h => hp.ne_zero (by ext i; simpa using congr_fun h i)
  have nonZ2 : (p 0 : â„‚) * z + p 1 â‰  0 := by simpa using linear_ne_zero z this
  simp only [coe_specialLinearGroup_apply, hg, algebraMap_int_eq, Int.coe_castRingHom,
    Complex.ofReal_intCast]
  field_simp [nonZ1, nonZ2, denom_ne_zero]
  rw [(by simp :
    (p 1 : â„‚) * z - p 0 = (p 1 * z - p 0) * â†‘(Matrix.det (â†‘g : Matrix (Fin 2) (Fin 2) â„¤)))]
  rw [â† hg, det_fin_two]
  simp only [Int.cast_sub, Int.cast_mul]
  ring

theorem tendsto_abs_re_smul {p : Fin 2 â†’ â„¤} (hp : IsCoprime (p 0) (p 1)) :
    Tendsto
      (fun g : { g : SL(2, â„¤) // g 1 = p } => |((g : SL(2, â„¤)) â€¢ z).re|) cofinite atTop := by
  suffices
    Tendsto (fun g : (fun g : SL(2, â„¤) => g 1) â»Â¹' {p} => ((g : SL(2, â„¤)) â€¢ z).re) cofinite
      (cocompact â„)
    by exact tendsto_norm_cocompact_atTop.comp this
  have : ((p 0 : â„) ^ 2 + (p 1 : â„) ^ 2)â»Â¹ â‰  0 := by
    apply inv_ne_zero
    exact mod_cast hp.sq_add_sq_ne_zero
  let f := Homeomorph.mulRightâ‚€ _ this
  let ff := Homeomorph.addRight
    (((p 1 : â„‚) * z - p 0) / (((p 0 : â„‚) ^ 2 + (p 1 : â„‚) ^ 2) * (p 0 * z + p 1))).re
  convert (f.trans ff).isClosedEmbedding.tendsto_cocompact.comp (tendsto_lcRow0 hp) with _ _ g
  change
    ((g : SL(2, â„¤)) â€¢ z).re =
      lcRow0 p â†‘(â†‘g : SL(2, â„)) / ((p 0 : â„) ^ 2 + (p 1 : â„) ^ 2) +
        Complex.re (((p 1 : â„‚) * z - p 0) / (((p 0 : â„‚) ^ 2 + (p 1 : â„‚) ^ 2) * (p 0 * z + p 1)))
  exact mod_cast congr_arg Complex.re (smul_eq_lcRow0_add z hp g.2)

end TendstoLemmas

section FundamentalDomain


attribute [local simp] UpperHalfPlane.coe_specialLinearGroup_apply

/-- For `z : â„`, there is a `g : SL(2,â„¤)` maximizing `(gâ€¢z).im` -/
theorem exists_max_im : âˆƒ g : SL(2, â„¤), âˆ€ g' : SL(2, â„¤), (g' â€¢ z).im â‰¤ (g â€¢ z).im := by
  classical
  let s : Set (Fin 2 â†’ â„¤) := {cd | IsCoprime (cd 0) (cd 1)}
  have hs : s.Nonempty := âŸ¨![1, 1], isCoprime_one_leftâŸ©
  obtain âŸ¨p, hp_coprime, hpâŸ© :=
    Filter.Tendsto.exists_within_forall_le hs (tendsto_normSq_coprime_pair z)
  obtain âŸ¨g, -, hgâŸ© := bottom_row_surj hp_coprime
  refine âŸ¨g, fun g' => ?_âŸ©
  rw [ModularGroup.im_smul_eq_div_normSq, ModularGroup.im_smul_eq_div_normSq,
    div_le_div_iff_of_pos_left]
  Â· simpa [â† hg] using hp (g' 1) (bottom_row_coprime g')
  Â· exact z.im_pos
  Â· exact normSq_denom_pos g' z.im_ne_zero
  Â· exact normSq_denom_pos g z.im_ne_zero

/-- Given `z : â„` and a bottom row `(c,d)`, among the `g : SL(2,â„¤)` with this bottom row, minimize
  `|(gâ€¢z).re|`. -/
theorem exists_row_one_eq_and_min_re {cd : Fin 2 â†’ â„¤} (hcd : IsCoprime (cd 0) (cd 1)) :
    âˆƒ g : SL(2, â„¤), g 1 = cd âˆ§ âˆ€ g' : SL(2, â„¤), g 1 = g' 1 â†’
      |(g â€¢ z).re| â‰¤ |(g' â€¢ z).re| := by
  haveI : Nonempty { g : SL(2, â„¤) // g 1 = cd } :=
    let âŸ¨x, hxâŸ© := bottom_row_surj hcd
    âŸ¨âŸ¨x, hx.2âŸ©âŸ©
  obtain âŸ¨g, hgâŸ© := Filter.Tendsto.exists_forall_le (tendsto_abs_re_smul z hcd)
  refine âŸ¨g, g.2, ?_âŸ©
  intro g1 hg1
  have : g1 âˆˆ (fun g : SL(2, â„¤) => g 1) â»Â¹' {cd} := by
    rw [Set.mem_preimage, Set.mem_singleton_iff]
    exact Eq.trans hg1.symm (Set.mem_singleton_iff.mp (Set.mem_preimage.mp g.2))
  exact hg âŸ¨g1, thisâŸ©

theorem coe_T_zpow_smul_eq {n : â„¤} : (â†‘(T ^ n â€¢ z) : â„‚) = z + n := by
  rw [UpperHalfPlane.coe_specialLinearGroup_apply]
  simp [coe_T_zpow, denom, num, -map_zpow]

theorem re_T_zpow_smul (n : â„¤) : (T ^ n â€¢ z).re = z.re + n := by
  rw [â† coe_re, coe_T_zpow_smul_eq, add_re, intCast_re, coe_re]

theorem im_T_zpow_smul (n : â„¤) : (T ^ n â€¢ z).im = z.im := by
  rw [â† coe_im, coe_T_zpow_smul_eq, add_im, intCast_im, add_zero, coe_im]

theorem re_T_smul : (T â€¢ z).re = z.re + 1 := by simpa using re_T_zpow_smul z 1

theorem im_T_smul : (T â€¢ z).im = z.im := by simpa using im_T_zpow_smul z 1

theorem re_T_inv_smul : (Tâ»Â¹ â€¢ z).re = z.re - 1 := by simpa using re_T_zpow_smul z (-1)

theorem im_T_inv_smul : (Tâ»Â¹ â€¢ z).im = z.im := by simpa using im_T_zpow_smul z (-1)

variable {z}

-- If instead we had `g` and `T` of type `PSL(2, â„¤)`, then we could simply state `g = T^n`.
theorem exists_eq_T_zpow_of_c_eq_zero (hc : g 1 0 = 0) :
    âˆƒ n : â„¤, âˆ€ z : â„, g â€¢ z = T ^ n â€¢ z := by
  have had := g.det_coe
  replace had : g 0 0 * g 1 1 = 1 := by rw [det_fin_two, hc] at had; omega
  rcases Int.eq_one_or_neg_one_of_mul_eq_one' had with (âŸ¨ha, hdâŸ© | âŸ¨ha, hdâŸ©)
  Â· use g 0 1
    suffices g = T ^ g 0 1 by intro z; conv_lhs => rw [this]
    ext i j; fin_cases i <;> fin_cases j <;>
      simp [ha, hc, hd, coe_T_zpow, show (1 : Fin (0 + 2)) = (1 : Fin 2) from rfl]
  Â· use -(g 0 1)
    suffices g = -T ^ (-(g 0 1)) by intro z; conv_lhs => rw [this, SL_neg_smul]
    ext i j; fin_cases i <;> fin_cases j <;>
      simp [ha, hc, hd, coe_T_zpow, show (1 : Fin (0 + 2)) = (1 : Fin 2) from rfl]

-- If `c = 1`, then `g` factorises into a product terms involving only `T` and `S`.
theorem g_eq_of_c_eq_one (hc : g 1 0 = 1) : g = T ^ g 0 0 * S * T ^ g 1 1 := by
  have hg := g.det_coe.symm
  replace hg : g 0 1 = g 0 0 * g 1 1 - 1 := by rw [det_fin_two, hc] at hg; omega
  refine Subtype.ext ?_
  conv_lhs => rw [(g : Matrix _ _ â„¤).eta_fin_two]
  simp only [hg, sub_eq_add_neg, hc, coe_mul, coe_T_zpow, coe_S, mul_fin_two, mul_zero, mul_one,
    zero_add, one_mul, add_zero, zero_mul]

/-- If `1 < |z|`, then `|S â€¢ z| < 1`. -/
theorem normSq_S_smul_lt_one (h : 1 < normSq z) : normSq â†‘(S â€¢ z) < 1 := by
  rw [UpperHalfPlane.coe_specialLinearGroup_apply]
  simpa [coe_S, num, denom] using (inv_lt_invâ‚€ z.normSq_pos zero_lt_one).mpr h

/-- If `|z| < 1`, then applying `S` strictly decreases `im`. -/
theorem im_lt_im_S_smul (h : normSq z < 1) : z.im < (S â€¢ z).im := by
  rw [ModularGroup.im_smul_eq_div_normSq]
  have : z.im < z.im / normSq (z : â„‚) := by
    have imz : 0 < z.im := im_pos z
    apply (lt_div_iffâ‚€ z.normSq_pos).mpr
    nlinarith
  simpa [denom, coe_S, SpecialLinearGroup.toGL]

/-- The standard (closed) fundamental domain of the action of `SL(2,â„¤)` on `â„`. -/
def fd : Set â„ :=
  {z | 1 â‰¤ normSq (z : â„‚) âˆ§ |z.re| â‰¤ (1 : â„) / 2}

/-- The standard open fundamental domain of the action of `SL(2,â„¤)` on `â„`. -/
def fdo : Set â„ :=
  {z | 1 < normSq (z : â„‚) âˆ§ |z.re| < (1 : â„) / 2}

@[inherit_doc ModularGroup.fd]
scoped[Modular] notation "ğ’Ÿ" => ModularGroup.fd

@[inherit_doc ModularGroup.fdo]
scoped[Modular] notation "ğ’Ÿáµ’" => ModularGroup.fdo

open scoped Modular

theorem abs_two_mul_re_lt_one_of_mem_fdo (h : z âˆˆ ğ’Ÿáµ’) : |2 * z.re| < 1 := by
  rw [abs_mul, abs_two, â† lt_div_iffâ‚€' (zero_lt_two' â„)]
  exact h.2

theorem three_lt_four_mul_im_sq_of_mem_fdo (h : z âˆˆ ğ’Ÿáµ’) : 3 < 4 * z.im ^ 2 := by
  have : 1 < z.re * z.re + z.im * z.im := by simpa [Complex.normSq_apply] using h.1
  have := h.2
  cases abs_cases z.re <;> nlinarith

/-- non-strict variant of `ModularGroup.three_le_four_mul_im_sq_of_mem_fdo` -/
theorem three_le_four_mul_im_sq_of_mem_fd {Ï„ : â„} (h : Ï„ âˆˆ ğ’Ÿ) : 3 â‰¤ 4 * Ï„.im ^ 2 := by
  have : 1 â‰¤ Ï„.re * Ï„.re + Ï„.im * Ï„.im := by simpa [Complex.normSq_apply] using h.1
  cases abs_cases Ï„.re <;> nlinarith [h.2]

/-- If `z âˆˆ ğ’Ÿáµ’`, and `n : â„¤`, then `|z + n| > 1`. -/
theorem one_lt_normSq_T_zpow_smul (hz : z âˆˆ ğ’Ÿáµ’) (n : â„¤) : 1 < normSq (T ^ n â€¢ z : â„) := by
  rw [coe_T_zpow_smul_eq]
  have hzâ‚ : 1 < z.re * z.re + z.im * z.im := hz.1
  have hzn := Int.nneg_mul_add_sq_of_abs_le_one n (abs_two_mul_re_lt_one_of_mem_fdo hz).le
  have : 1 < (z.re + â†‘n) * (z.re + â†‘n) + z.im * z.im := by linarith
  simpa [normSq, num, denom]

theorem eq_zero_of_mem_fdo_of_T_zpow_mem_fdo {n : â„¤} (hz : z âˆˆ ğ’Ÿáµ’) (hg : T ^ n â€¢ z âˆˆ ğ’Ÿáµ’) :
    n = 0 := by
  suffices |(n : â„)| < 1 by
    rwa [â† Int.cast_abs, â† Int.cast_one, Int.cast_lt, Int.abs_lt_one_iff] at this
  have hâ‚ := hz.2
  have hâ‚‚ := hg.2
  rw [re_T_zpow_smul] at hâ‚‚
  calc
    |(n : â„)| â‰¤ |z.re| + |z.re + (n : â„)| := abs_add' (n : â„) z.re
    _ < 1 / 2 + 1 / 2 := add_lt_add hâ‚ hâ‚‚
    _ = 1 := add_halves 1

/-- First Fundamental Domain Lemma: Any `z : â„` can be moved to `ğ’Ÿ` by an element of
`SL(2,â„¤)` -/
theorem exists_smul_mem_fd (z : â„) : âˆƒ g : SL(2, â„¤), g â€¢ z âˆˆ ğ’Ÿ := by
  -- obtain a gâ‚€ which maximizes im (g â€¢ z),
  obtain âŸ¨gâ‚€, hgâ‚€âŸ© := exists_max_im z
  -- then among those, minimize re
  obtain âŸ¨g, hg, hg'âŸ© := exists_row_one_eq_and_min_re z (bottom_row_coprime gâ‚€)
  refine âŸ¨g, ?_âŸ©
  -- `g` has same max im property as `gâ‚€`
  have hgâ‚€' : âˆ€ g' : SL(2, â„¤), (g' â€¢ z).im â‰¤ (g â€¢ z).im := by
    have hg'' : (g â€¢ z).im = (gâ‚€ â€¢ z).im := by
      rw [ModularGroup.im_smul_eq_div_normSq, ModularGroup.im_smul_eq_div_normSq,
        denom_apply, denom_apply, hg]
    simpa only [hg''] using hgâ‚€
  constructor
  Â· -- Claim: `1 â‰¤ â‡‘norm_sq â†‘(g â€¢ z)`. If not, then `Sâ€¢gâ€¢z` has larger imaginary part
    contrapose! hgâ‚€'
    refine âŸ¨S * g, ?_âŸ©
    rw [mul_smul]
    exact im_lt_im_S_smul hgâ‚€'
  Â· show |(g â€¢ z).re| â‰¤ 1 / 2
    -- if not, then either `T` or `T'` decrease |Re|.
    rw [abs_le]
    constructor
    Â· contrapose! hg'
      refine âŸ¨T * g, (T_mul_apply_one _).symm, ?_âŸ©
      rw [mul_smul, re_T_smul]
      cases abs_cases ((g â€¢ z).re + 1) <;> cases abs_cases (g â€¢ z).re <;> linarith
    Â· contrapose! hg'
      refine âŸ¨Tâ»Â¹ * g, (T_inv_mul_apply_one _).symm, ?_âŸ©
      rw [mul_smul, re_T_inv_smul]
      cases abs_cases ((g â€¢ z).re - 1) <;> cases abs_cases (g â€¢ z).re <;> linarith

section UniqueRepresentative

/-- An auxiliary result en route to `ModularGroup.c_eq_zero`. -/
theorem abs_c_le_one (hz : z âˆˆ ğ’Ÿáµ’) (hg : g â€¢ z âˆˆ ğ’Ÿáµ’) : |g 1 0| â‰¤ 1 := by
  let c' : â„¤ := g 1 0
  let c := (c' : â„)
  suffices 3 * c ^ 2 < 4 by
    rw [â† Int.cast_pow, â† Int.cast_three, â† Int.cast_four, â† Int.cast_mul, Int.cast_lt] at this
    replace this : c' ^ 2 â‰¤ 1 ^ 2 := by omega
    rwa [sq_le_sq, abs_one] at this
  suffices c â‰  0 â†’ 9 * c ^ 4 < 16 by
    rcases eq_or_ne c 0 with (hc | hc)
    Â· rw [hc]; norm_num
    Â· refine (abs_lt_of_sq_lt_sq' ?_ (by norm_num)).2
      specialize this hc
      linarith
  intro hc
  have hâ‚ : 3 * 3 * c ^ 4 < 4 * (g â€¢ z).im ^ 2 * (4 * z.im ^ 2) * c ^ 4 := by
    gcongr <;> apply three_lt_four_mul_im_sq_of_mem_fdo <;> assumption
  have hâ‚‚ : (c * z.im) ^ 4 / normSq (denom (â†‘g) z) ^ 2 â‰¤ 1 :=
    div_le_one_of_leâ‚€
      (pow_four_le_pow_two_of_pow_two_le (z.c_mul_im_sq_le_normSq_denom g))
      (sq_nonneg _)
  let nsq := normSq (denom g z)
  calc
    9 * c ^ 4 < c ^ 4 * z.im ^ 2 * (g â€¢ z).im ^ 2 * 16 := by linarith
    _ = c ^ 4 * z.im ^ 4 / nsq ^ 2 * 16 := by
      rw [im_smul_eq_div_normSq, div_pow]
      ring
    _ â‰¤ 16 := by rw [â† mul_pow]; linarith

/-- An auxiliary result en route to `ModularGroup.eq_smul_self_of_mem_fdo_mem_fdo`. -/
theorem c_eq_zero (hz : z âˆˆ ğ’Ÿáµ’) (hg : g â€¢ z âˆˆ ğ’Ÿáµ’) : g 1 0 = 0 := by
  have hp : âˆ€ {g' : SL(2, â„¤)}, g' â€¢ z âˆˆ ğ’Ÿáµ’ â†’ g' 1 0 â‰  1 := by
    intro g' hg'
    by_contra hc
    let a := g' 0 0
    let d := g' 1 1
    have had : T ^ (-a) * g' = S * T ^ d := by
      rw [g_eq_of_c_eq_one hc]
      dsimp [a, d]
      group
    let w := T ^ (-a) â€¢ g' â€¢ z
    have hâ‚ : w = S â€¢ T ^ d â€¢ z := by simp only [w, â† mul_smul, had]
    replace hâ‚ : normSq w < 1 := hâ‚.symm â–¸ normSq_S_smul_lt_one (one_lt_normSq_T_zpow_smul hz d)
    have hâ‚‚ : 1 < normSq w := one_lt_normSq_T_zpow_smul hg' (-a)
    linarith
  have hn : g 1 0 â‰  -1 := by
    intro hc
    replace hc : (-g) 1 0 = 1 := by simp [â† neg_eq_iff_eq_neg.mpr hc]
    replace hg : -g â€¢ z âˆˆ ğ’Ÿáµ’ := (SL_neg_smul g z).symm â–¸ hg
    exact hp hg hc
  specialize hp hg
  rcases Int.abs_le_one_iff.mp <| abs_c_le_one hz hg with âŸ¨âŸ© <;> tauto

/-- Second Fundamental Domain Lemma: if both `z` and `g â€¢ z` are in the open domain `ğ’Ÿáµ’`,
where `z : â„` and `g : SL(2,â„¤)`, then `z = g â€¢ z`. -/
theorem eq_smul_self_of_mem_fdo_mem_fdo (hz : z âˆˆ ğ’Ÿáµ’) (hg : g â€¢ z âˆˆ ğ’Ÿáµ’) : z = g â€¢ z := by
  obtain âŸ¨n, hnâŸ© := exists_eq_T_zpow_of_c_eq_zero (c_eq_zero hz hg)
  rw [hn] at hg âŠ¢
  simp [eq_zero_of_mem_fdo_of_T_zpow_mem_fdo hz hg, one_smul]

end UniqueRepresentative

end FundamentalDomain

lemma exists_one_half_le_im_smul (Ï„ : â„) : âˆƒ Î³ : SL(2, â„¤), 1 / 2 â‰¤ im (Î³ â€¢ Ï„) := by
  obtain âŸ¨Î³, hÎ³âŸ© := exists_smul_mem_fd Ï„
  use Î³
  nlinarith [three_le_four_mul_im_sq_of_mem_fd hÎ³, im_pos (Î³ â€¢ Ï„)]

/-- For every `Ï„ : â„` there is some `Î³ âˆˆ SL(2, â„¤)` that sends it to an element whose
imaginary part is at least `1/2` and such that `denom Î³ Ï„` has norm at most 1. -/
lemma exists_one_half_le_im_smul_and_norm_denom_le (Ï„ : â„) :
    âˆƒ Î³ : SL(2, â„¤), 1 / 2 â‰¤ im (Î³ â€¢ Ï„) âˆ§ â€–denom Î³ Ï„â€– â‰¤ 1 := by
  rcases le_total (1 / 2) Ï„.im with h | h
  Â· exact âŸ¨1, (one_smul SL(2, â„¤) Ï„).symm â–¸ h, by
      simp only [map_one, denom_one, norm_one, le_refl]âŸ©
  Â· refine (exists_one_half_le_im_smul Ï„).imp (fun Î³ hÎ³ â†¦ âŸ¨hÎ³, ?_âŸ©)
    have h1 : Ï„.im â‰¤ (Î³ â€¢ Ï„).im := h.trans hÎ³
    rw [im_smul_eq_div_normSq, le_div_iffâ‚€ (normSq_denom_pos Î³ Ï„.im_ne_zero),
      normSq_eq_norm_sq] at h1
    simpa only [sq_le_one_iff_abs_le_one, abs_norm] using
      (mul_le_iff_le_one_right Ï„.2).mp h1

end ModularGroup
