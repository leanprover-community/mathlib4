/-
Copyright (c) 2024 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.MeasureTheory.Integral.FundThmCalculus

/-!
# Abel's summation formula

We prove several versions of Abel's summation formula.

## Results

* `sum_mul_eq_sub_sub_integral_mul`: general statement of the formula for a sum between two
(nonnegative) reals `a` and `b`.

* `sum_mul_eq_sub_integral_mul`: a specialized version of `sum_mul_eq_sub_sub_integral_mul` for
  the case `a = 0`.

 * `sum_mul_eq_sub_integral_mul`: a specialized version of `sum_mul_eq_sub_integral_mul` for
  when the first coefficient of the sequence is `0`. This is useful for `ArithmeticFunction`.

## References

* <https://en.wikipedia.org/wiki/Abel%27s_summation_formula>

-/

noncomputable section

open Finset intervalIntegral MeasureTheory IntervalIntegrable

/-- Abel's summation formula. -/
theorem sum_mul_eq_sub_sub_integral_mul {ğ•œ : Type*} [RCLike ğ•œ] (c : â„• â†’ ğ•œ) {f : â„ â†’ ğ•œ} {a b : â„}
  (ha : 0 â‰¤ a) (hab : a â‰¤ b)
    (hf_diff : âˆ€ t âˆˆ Set.Icc a b, DifferentiableAt â„ f t)
    (hf_int : IntervalIntegrable (deriv f) volume a b) :
    âˆ‘ k âˆˆ Ioc âŒŠaâŒ‹â‚Š âŒŠbâŒ‹â‚Š, f k * c k =
      f b * (âˆ‘ k âˆˆ Icc 0 âŒŠbâŒ‹â‚Š, c k) - f a * (âˆ‘ k âˆˆ Icc 0 âŒŠaâŒ‹â‚Š, c k) -
        âˆ« t in Set.Ioc a b, deriv f t * (âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k) := by
  rw [â† integral_of_le hab]
  -- We prove some inequalities to be used later on by linarith / positivity
  have : âŒŠaâŒ‹â‚Š â‰¤ a := Nat.floor_le ha
  have : a < âŒŠaâŒ‹â‚Š + 1 := Nat.lt_floor_add_one _
  have : b < âŒŠbâŒ‹â‚Š + 1 := Nat.lt_floor_add_one _
  -- The partial sum function is locally constant
  have h_sumlocc : âˆ€ (n : â„•), âˆ€áµ t, t âˆˆ Set.Icc (n : â„) (n + 1) â†’
      âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k = âˆ‘ k âˆˆ Icc 0 n, c k := fun n â†¦ by
    filter_upwards[Ico_ae_eq_Icc (a := (n : â„)) (b := n + 1)] with t h ht
    rw [Nat.floor_eq_on_Ico _ _ (h.mpr ht)]
  -- Thus, we can integrate it
  have h_integ : âˆ€ (tâ‚ tâ‚‚ : â„) (n : â„•) (_ : Set.uIoc tâ‚ tâ‚‚ âŠ† Set.Icc n (n + 1))
      (_ : Set.uIcc tâ‚ tâ‚‚ âŠ† Set.Icc a b),
      âˆ« t in tâ‚..tâ‚‚, deriv f t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k = (f tâ‚‚ - f tâ‚) * âˆ‘ k âˆˆ Icc 0 n, c k := by
    intro tâ‚ tâ‚‚ n htâ‚ htâ‚‚
    rw [â† integral_deriv_eq_sub (fun t ht â†¦ hf_diff _ (htâ‚‚ ht)) (hf_int.mono_set
      (by rwa [Set.uIcc_of_le hab])), â† integral_mul_const]
    refine integral_congr_ae ?_
    filter_upwards [h_sumlocc n] with t h h' using by rw [h (htâ‚ h')]
  -- We consider two cases depending on whether the sum is empty or not
  obtain hb | hb := eq_or_lt_of_le (Nat.floor_le_floor hab)
  Â· rw [hb, Ioc_eq_empty_of_le le_rfl, sum_empty, â† sub_mul, h_integ, sub_self]
    Â· rw [Set.uIoc_of_le hab]
      exact Set.Ioc_subset_Icc_self.trans <|
        Set.Icc_subset_Icc (by rw [â† hb]; linarith) (by linarith)
    Â· rw [Set.uIcc_of_le hab]
  -- Some more inequalities for linarith / positivity
  have : 1 â‰¤ b := Nat.floor_pos.mp (by linarith)
  have : âŒŠbâŒ‹â‚Š â‰¤ b := Nat.floor_le (by positivity)
  have : âŒŠaâŒ‹â‚Š + 1 â‰¤ b := by rwa [â† Nat.cast_add_one,  â† Nat.le_floor_iff (by positivity)]
  have : a < âŒŠbâŒ‹â‚Š := by rwa [â† Nat.floor_lt ha]
  -- And then some additional properties
  have h_Icck : âˆ€ â¦ƒkâ¦„, k âˆˆ Set.Ico (âŒŠaâŒ‹â‚Š + 1) âŒŠbâŒ‹â‚Š â†’ Set.Icc (k : â„) (k + 1) âŠ† Set.Icc a b := by
    refine fun k hk â†¦ Set.Icc_subset_Icc ?_ ?_
    Â· have := (Nat.succ_eq_add_one _) â–¸ (Set.mem_Ico.mp hk).1
      exact le_of_lt <| (Nat.floor_lt' (by linarith)).mp this
    Â· rw [â† Nat.cast_add_one, â† Nat.le_floor_iff' (by linarith)]
      exact (Set.mem_Ico.mp hk).2
  have h_locint : âˆ€ (tâ‚ tâ‚‚ : â„) (n : â„•) (_ : Set.uIoc tâ‚ tâ‚‚ âŠ† Set.Icc n (n + 1))
      (_ : Set.uIcc tâ‚ tâ‚‚ âŠ† Set.Icc a b),
      IntervalIntegrable (fun t â†¦ deriv f t * (âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k)) volume tâ‚ tâ‚‚ := by
    refine fun tâ‚ tâ‚‚ n htâ‚ htâ‚‚ â†¦ ((hf_int.mul_const (âˆ‘ k âˆˆ Icc 0 n, c k)).mono_set
      ((Set.uIcc_of_le (by linarith : a â‰¤ b)) â–¸ htâ‚‚)).congr ?_
    refine ae_restrict_of_ae_restrict_of_subset htâ‚ <| (ae_restrict_iff' measurableSet_Icc).mpr ?_
    filter_upwards [h_sumlocc n] with t htâ‚ htâ‚‚ using by rw [htâ‚ htâ‚‚]
  have h_int : IntervalIntegrable (fun t â†¦ deriv f t * (âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k)) volume a b := by
    refine (h_locint a (âŒŠaâŒ‹â‚Š + 1 : â„•) âŒŠaâŒ‹â‚Š ?_ ?_).trans <|
      (trans_iterate_Ico hb fun k hk â†¦ h_locint k _ k ?_ ?_).trans (h_locint âŒŠbâŒ‹â‚Š b âŒŠbâŒ‹â‚Š ?_ ?_)
    Â· rw [Nat.cast_add_one, Set.uIoc_of_le (by linarith)]
      exact Set.Ioc_subset_Icc_self.trans (Set.Icc_subset_Icc_left (by linarith))
    Â· rw [Nat.cast_add_one, Set.uIcc_of_le (by linarith)]
      exact Set.Icc_subset_Icc_right (by linarith)
    Â· rw [Set.uIoc_of_le (by simp), Nat.cast_add_one]
      exact Set.Ioc_subset_Icc_self
    Â· rw [Set.uIcc_of_le (by simp), Nat.cast_add_one]
      exact h_Icck hk
    Â· rw [Set.uIoc_of_le (by linarith)]
      exact Set.Ioc_subset_Icc_self.trans <| Set.Icc_subset_Icc_right (by linarith)
    Â· rw [Set.uIcc_of_le (by linarith)]
      exact Set.Icc_subset_Icc_left (by linarith)
  simp_rw [â† smul_eq_mul, sum_Ioc_by_parts (fun k â†¦ f k) _ hb, range_eq_Ico, Nat.Ico_succ_right,
    smul_eq_mul]
  rw [show âˆ‘ k âˆˆ Ioc âŒŠaâŒ‹â‚Š (âŒŠbâŒ‹â‚Š - 1), (f â†‘(k + 1) - f â†‘k) * âˆ‘ n âˆˆ Icc 0 k, c n =
    âˆ‘ k âˆˆ Ioc âŒŠaâŒ‹â‚Š (âŒŠbâŒ‹â‚Š - 1), âˆ« (t : â„) in â†‘k..â†‘(k + 1), deriv f t * âˆ‘ n âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c n by
      refine sum_congr rfl fun k _ â†¦ (h_integ _ _ _ (by simp [Set.Ioc_subset_Icc_self]) ?_).symm
      rw [Set.uIcc_of_le (by simp), Nat.cast_add_one]
      refine h_Icck ?_
      rwa [â† Nat.sub_add_cancel (by linarith : 1 â‰¤ âŒŠbâŒ‹â‚Š), â† Finset.coe_Ico, Nat.Ico_succ_succ],
    â† Nat.Ico_succ_succ, Nat.succ_eq_add_one, Nat.succ_eq_add_one,
    tsub_add_cancel_of_le (by linarith), sum_integral_adjacent_intervals_Ico (by linarith),
    Nat.cast_add, Nat.cast_one, â† integral_interval_sub_left (a := a) (c := âŒŠaâŒ‹â‚Š + 1),
    â† integral_add_adjacent_intervals (b := âŒŠbâŒ‹â‚Š) (c := b), h_integ a (âŒŠaâŒ‹â‚Š + 1) âŒŠaâŒ‹â‚Š,
    h_integ âŒŠbâŒ‹â‚Š b âŒŠbâŒ‹â‚Š]
  Â· ring
  -- Now, we just need to check all the technical conditions
  Â· rw [Set.uIoc_of_le (by linarith)]
    exact Set.Ioc_subset_Icc_self.trans <| Set.Icc_subset_Icc_right (by linarith)
  Â· rw [Set.uIcc_of_le (by linarith)]
    exact Set.Icc_subset_Icc_left (by linarith)
  Â· rw [Set.uIoc_of_le (by linarith)]
    exact Set.Ioc_subset_Icc_self.trans <| Set.Icc_subset_Icc_left (by linarith)
  Â· rw [Set.uIcc_of_le (by linarith)]
    exact Set.Icc_subset_Icc_right (by linarith)
  Â· refine h_int.mono_set ?_
    rw [Set.uIcc_of_le (by linarith), Set.uIcc_of_le (by linarith)]
    exact Set.Icc_subset_Icc_right (by linarith)
  Â· refine h_int.mono_set ?_
    rw [Set.uIcc_of_le (by linarith), Set.uIcc_of_le (by linarith)]
    exact Set.Icc_subset_Icc_left (by linarith)
  Â· refine h_int.mono_set ?_
    rw [Set.uIcc_of_le (by linarith), Set.uIcc_of_le (by linarith)]
    exact Set.Icc_subset_Icc_right (by linarith)
  Â· refine h_int.mono_set ?_
    rw [Set.uIcc_of_le (by linarith), Set.uIcc_of_le (by linarith)]
    exact Set.Icc_subset_Icc_right (by linarith)
  Â· refine fun k hk â†¦ h_int.mono_set ?_
    rw [Set.uIcc_of_le (by simp), Set.uIcc_of_le (by linarith), Nat.cast_add_one]
    exact h_Icck hk

/-- Specialized version of `sum_mul_eq_sub_sub_integral_mul` for the case `a = 0`.-/
theorem sum_mul_eq_sub_integral_mul {ğ•œ : Type*} [RCLike ğ•œ] (c : â„• â†’ ğ•œ) {f : â„ â†’ ğ•œ} {b : â„}
    (hb : 0 â‰¤ b) (hf_diff : âˆ€ t âˆˆ Set.Icc 0 b, DifferentiableAt â„ f t)
    (hf_int : IntervalIntegrable (deriv f) volume 0 b) :
    âˆ‘ k âˆˆ Icc 0 âŒŠbâŒ‹â‚Š, f k * c k =
      f b * (âˆ‘ k âˆˆ Icc 0 âŒŠbâŒ‹â‚Š, c k) - âˆ« t in Set.Ioc 0 b, deriv f t * (âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k) := by
  nth_rewrite 1 [Finset.Icc_eq_cons_Ioc (Nat.zero_le _)]
  rw [sum_cons, â† Nat.floor_zero (Î± := â„), sum_mul_eq_sub_sub_integral_mul c le_rfl hb hf_diff
    hf_int, Nat.floor_zero, Nat.cast_zero, Icc_self, sum_singleton]
  ring

/-- Specialized version of `sum_mul_eq_sub_integral_mul` when the first coefficient of the sequence
`c` is equal to `0`. -/
theorem sum_mul_eq_sub_integral_mul' {ğ•œ : Type*} [RCLike ğ•œ] (c : â„• â†’ ğ•œ) (hc : c 0 = 0)
    {f : â„ â†’ ğ•œ} (b : â„) (hf_diff : âˆ€ t âˆˆ Set.Icc 1 b, DifferentiableAt â„ f t)
    (hf_int : IntervalIntegrable (deriv f) volume 1 b) :
    âˆ‘ k âˆˆ Icc 0 âŒŠbâŒ‹â‚Š, f k * c k =
      f b * (âˆ‘ k âˆˆ Icc 0 âŒŠbâŒ‹â‚Š, c k) - âˆ« t in Set.Ioc 1 b, deriv f t * (âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k) := by
  obtain hb | hb := le_or_gt 1 b
  Â· have : 1 â‰¤ âŒŠbâŒ‹â‚Š := (Nat.one_le_floor_iff _).mpr hb
    nth_rewrite 1 [Finset.Icc_eq_cons_Ioc (by linarith), sum_cons, â† Nat.Icc_succ_left,
      Finset.Icc_eq_cons_Ioc (by linarith), sum_cons]
    rw [Nat.succ_eq_add_one, zero_add, â† Nat.floor_one (Î± := â„),
      sum_mul_eq_sub_sub_integral_mul c zero_le_one hb hf_diff hf_int, Nat.floor_one, Nat.cast_one,
      Finset.Icc_eq_cons_Ioc zero_le_one, sum_cons, show 1 = 0 + 1 by rfl, Nat.Ioc_succ_singleton,
      zero_add, sum_singleton, hc, mul_zero, zero_add]
    ring
  Â· simp_rw [Nat.floor_eq_zero.mpr hb, Icc_self, sum_singleton, Nat.cast_zero, hc, mul_zero,
    Set.Ioc_eq_empty_of_le hb.le, Measure.restrict_empty, integral_zero_measure, sub_self]
