/-
Copyright (c) 2024 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.MeasureTheory.Integral.FundThmCalculus

/-!
# Abel's summation formula

We prove several versions of Abel's summation formula.

## Results

* `sum_mul_eq_sub_sub_integral_mul`: general statement of the formula for a sum between two
(nonnegative) reals `a` and `b`.

* `sum_mul_eq_sub_integral_mul`: a specialized version of `sum_mul_eq_sub_sub_integral_mul` for
  the case `a = 0`.

 * `sum_mul_eq_sub_integral_mul`: a specialized version of `sum_mul_eq_sub_integral_mul` for
  when the first coefficient of the sequence is `0`. This is useful for `ArithmeticFunction`.

## References

* <https://en.wikipedia.org/wiki/Abel%27s_summation_formula>

-/

noncomputable section

open Finset intervalIntegral MeasureTheory IntervalIntegrable

variable {ğ•œ : Type*} [RCLike ğ•œ] (c : â„• â†’ ğ•œ) {f : â„ â†’ ğ•œ} {a b : â„}

namespace abelSummationProof

private theorem sumlocc (n : â„•) :
    âˆ€áµ t, t âˆˆ Set.Icc (n : â„) (n + 1) â†’ âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k = âˆ‘ k âˆˆ Icc 0 n, c k := by
  filter_upwards [Ico_ae_eq_Icc] with t h ht
  rw [Nat.floor_eq_on_Ico _ _ (h.mpr ht)]

private theorem integralmulsum (hf_diff : âˆ€ t âˆˆ Set.Icc a b, DifferentiableAt â„ f t)
    (hf_int : IntegrableOn (deriv f) (Set.Icc a b)) (tâ‚ tâ‚‚ : â„) (n : â„•) (h : tâ‚ â‰¤ tâ‚‚)
    (hâ‚ : n â‰¤ tâ‚) (hâ‚‚ : tâ‚‚ â‰¤ n + 1) (hâ‚ƒ : a â‰¤ tâ‚) (hâ‚„ : tâ‚‚ â‰¤ b) :
    âˆ« t in tâ‚..tâ‚‚, deriv f t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k =
      (f tâ‚‚ - f tâ‚) * âˆ‘ k âˆˆ Icc 0 n, c k := by
  have h_incâ‚ : Î™ tâ‚ tâ‚‚ âŠ† Set.Icc n (n + 1) :=
    Set.uIoc_of_le h â–¸ Set.Ioc_subset_Icc_self.trans <| Set.Icc_subset_Icc hâ‚ hâ‚‚
  have h_incâ‚‚ : Set.uIcc tâ‚ tâ‚‚ âŠ† Set.Icc a b := Set.uIcc_of_le h â–¸ Set.Icc_subset_Icc hâ‚ƒ hâ‚„
  rw [â† integral_deriv_eq_sub (fun t ht â†¦ hf_diff t (h_incâ‚‚ ht)), â† integral_mul_const]
  Â· refine integral_congr_ae ?_
    filter_upwards [sumlocc c n] with t h h'
    rw [h (h_incâ‚ h')]
  Â· refine (intervalIntegrable_iff_integrableOn_Icc_of_le h).mpr (hf_int.mono_set ?_)
    rwa [â† Set.uIcc_of_le h]

private theorem ineqofmemIco {k : â„•} (hk : k âˆˆ Set.Ico (âŒŠaâŒ‹â‚Š + 1) âŒŠbâŒ‹â‚Š) :
    a â‰¤ k âˆ§ k + 1 â‰¤ b := by
  constructor
  Â· have := (Set.mem_Ico.mp hk).1
    exact le_of_lt <| (Nat.floor_lt' (by omega)).mp this
  Â· rw [â† Nat.cast_add_one, â† Nat.le_floor_iff' (Nat.succ_ne_zero k)]
    exact (Set.mem_Ico.mp hk).2

private theorem ineqofmemIco' {k : â„•} (hk : k âˆˆ Ico (âŒŠaâŒ‹â‚Š + 1) âŒŠbâŒ‹â‚Š) :
    a â‰¤ k âˆ§ k + 1 â‰¤ b :=
  ineqofmemIco (by rwa [â† Finset.coe_Ico])

private theorem integrablemulsum (ha : 0 â‰¤ a) (hb : âŒŠaâŒ‹â‚Š < âŒŠbâŒ‹â‚Š)
    (hf_int : IntegrableOn (deriv f) (Set.Icc a b)) :
    IntegrableOn (fun t â†¦ deriv f t * (âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k)) (Set.Icc a b) := by
  have h_locint {tâ‚ tâ‚‚ : â„} {n : â„•} (h : tâ‚ â‰¤ tâ‚‚) (hâ‚ : n â‰¤ tâ‚) (hâ‚‚ : tâ‚‚ â‰¤ n + 1)
      (hâ‚ƒ : a â‰¤ tâ‚) (hâ‚„ : tâ‚‚ â‰¤ b) :
      IntervalIntegrable (fun t â†¦ deriv f t * (âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k)) volume tâ‚ tâ‚‚ := by
    rw [intervalIntegrable_iff_integrableOn_Icc_of_le h]
    exact (IntegrableOn.mono_set (hf_int.mul_const _) (Set.Icc_subset_Icc hâ‚ƒ hâ‚„)).congr
      <| ae_restrict_of_ae_restrict_of_subset (Set.Icc_subset_Icc hâ‚ hâ‚‚)
        <| (ae_restrict_iff' measurableSet_Icc).mpr
          (by filter_upwards [sumlocc c n] with t h ht using by rw [h ht])
  have aux1 : 0 â‰¤ b := (Nat.pos_of_floor_pos <| (Nat.zero_le _).trans_lt hb).le
  have aux2 : âŒŠaâŒ‹â‚Š + 1 â‰¤ b := by rwa [â† Nat.cast_add_one, â† Nat.le_floor_iff aux1]
  have aux3 : a â‰¤ âŒŠaâŒ‹â‚Š + 1 := (Nat.lt_floor_add_one _).le
  have aux4 : a â‰¤ âŒŠbâŒ‹â‚Š := le_of_lt (by rwa [â† Nat.floor_lt ha])
  -- now break up into 3 subintervals
  rw [â† intervalIntegrable_iff_integrableOn_Icc_of_le (aux3.trans aux2)]
  have I1 : IntervalIntegrable _ volume a â†‘(âŒŠaâŒ‹â‚Š + 1) :=
    h_locint (mod_cast aux3) (Nat.floor_le ha) (mod_cast le_rfl) le_rfl (mod_cast aux2)
  have I2 : IntervalIntegrable _ volume â†‘(âŒŠaâŒ‹â‚Š + 1) âŒŠbâŒ‹â‚Š :=
    trans_iterate_Ico hb fun k hk â†¦ h_locint (mod_cast k.le_succ)
      le_rfl (mod_cast le_rfl) (ineqofmemIco hk).1 (mod_cast (ineqofmemIco hk).2)
  have I3 : IntervalIntegrable _ volume âŒŠbâŒ‹â‚Š b :=
    h_locint (Nat.floor_le aux1) le_rfl (Nat.lt_floor_add_one _).le aux4 le_rfl
  exact (I1.trans I2).trans I3

/-- Abel's summation formula. -/
theorem _root_.sum_mul_eq_sub_sub_integral_mul (ha : 0 â‰¤ a) (hab : a â‰¤ b)
    (hf_diff : âˆ€ t âˆˆ Set.Icc a b, DifferentiableAt â„ f t)
    (hf_int : IntegrableOn (deriv f) (Set.Icc a b)) :
    âˆ‘ k âˆˆ Ioc âŒŠaâŒ‹â‚Š âŒŠbâŒ‹â‚Š, f k * c k =
      f b * (âˆ‘ k âˆˆ Icc 0 âŒŠbâŒ‹â‚Š, c k) - f a * (âˆ‘ k âˆˆ Icc 0 âŒŠaâŒ‹â‚Š, c k) -
        âˆ« t in Set.Ioc a b, deriv f t * (âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k) := by
  rw [â† integral_of_le hab]
  have aux1 : âŒŠaâŒ‹â‚Š â‰¤ a := Nat.floor_le ha
  have aux2 : b â‰¤ âŒŠbâŒ‹â‚Š + 1 := (Nat.lt_floor_add_one _).le
  -- We consider two cases depending on whether the sum is empty or not
  obtain hb | hb := eq_or_lt_of_le (Nat.floor_le_floor hab)
  Â· rw [hb, Ioc_eq_empty_of_le le_rfl, sum_empty, â† sub_mul,
      integralmulsum c hf_diff hf_int _ _ âŒŠbâŒ‹â‚Š hab (hb â–¸ aux1) aux2 le_rfl le_rfl, sub_self]
  have aux3 : a â‰¤ âŒŠaâŒ‹â‚Š + 1 := (Nat.lt_floor_add_one _).le
  have aux4 : âŒŠaâŒ‹â‚Š + 1 â‰¤ b := by rwa [â† Nat.cast_add_one,  â† Nat.le_floor_iff (ha.trans hab)]
  have aux5 : âŒŠbâŒ‹â‚Š â‰¤ b := Nat.floor_le (ha.trans hab)
  have aux6 : a â‰¤ âŒŠbâŒ‹â‚Š := Nat.floor_lt ha |>.mp hb |>.le
  simp_rw [â† smul_eq_mul, sum_Ioc_by_parts (fun k â†¦ f k) _ hb, range_eq_Ico, Nat.Ico_succ_right,
    smul_eq_mul]
  have : âˆ‘ k âˆˆ Ioc âŒŠaâŒ‹â‚Š (âŒŠbâŒ‹â‚Š - 1), (f â†‘(k + 1) - f k) * âˆ‘ n âˆˆ Icc 0 k, c n =
        âˆ‘ k âˆˆ Ico (âŒŠaâŒ‹â‚Š + 1) âŒŠbâŒ‹â‚Š, âˆ« t in k..â†‘(k + 1), deriv f t * âˆ‘ n âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c n := by
    rw [â† Nat.Ico_succ_succ, Nat.succ_eq_add_one,  Nat.succ_eq_add_one, Nat.sub_add_cancel
      (by omega), Eq.comm]
    exact sum_congr rfl fun k hk â†¦ (integralmulsum c hf_diff hf_int _ _ _  (mod_cast k.le_succ)
      le_rfl (mod_cast le_rfl) (ineqofmemIco' hk).1 <| mod_cast (ineqofmemIco' hk).2)
  rw [this, sum_integral_adjacent_intervals_Ico hb, Nat.cast_add, Nat.cast_one,
    â† integral_interval_sub_left (a := a) (c := âŒŠaâŒ‹â‚Š + 1),
    â† integral_add_adjacent_intervals (b := âŒŠbâŒ‹â‚Š) (c := b),
    integralmulsum c hf_diff hf_int _ _ _ aux3 aux1 le_rfl le_rfl aux4,
    integralmulsum c hf_diff hf_int _ _ _ aux5 le_rfl aux2 aux6 le_rfl]
  Â· ring
  -- now deal with the integrability side goals
  -- (Note we have 5 goals, but the 1st and 3rd are identical. TODO: find a non-hacky way of dealing
  -- with both at once.)
  Â· rw [intervalIntegrable_iff_integrableOn_Icc_of_le aux6]
    exact (integrablemulsum c ha hb hf_int).mono_set (Set.Icc_subset_Icc_right aux5)
  Â· rw [intervalIntegrable_iff_integrableOn_Icc_of_le aux5]
    exact (integrablemulsum c ha hb hf_int).mono_set (Set.Icc_subset_Icc_left aux6)
  Â· rw [intervalIntegrable_iff_integrableOn_Icc_of_le aux6]
    exact (integrablemulsum c ha hb hf_int).mono_set (Set.Icc_subset_Icc_right aux5)
  Â· rw [intervalIntegrable_iff_integrableOn_Icc_of_le aux3]
    exact (integrablemulsum c ha hb hf_int).mono_set (Set.Icc_subset_Icc_right aux4)
  Â· exact fun k hk â†¦ (intervalIntegrable_iff_integrableOn_Icc_of_le (mod_cast k.le_succ)).mpr
      <| (integrablemulsum c ha hb hf_int).mono_set
        <| (Set.Icc_subset_Icc_iff (mod_cast k.le_succ)).mpr <| mod_cast (ineqofmemIco hk)

end abelSummationProof

/-- Specialized version of `sum_mul_eq_sub_sub_integral_mul` for the case `a = 0`.-/
theorem sum_mul_eq_sub_integral_mul {b : â„} (hb : 0 â‰¤ b)
    (hf_diff : âˆ€ t âˆˆ Set.Icc 0 b, DifferentiableAt â„ f t)
    (hf_int : IntegrableOn (deriv f) (Set.Icc 0 b)) :
    âˆ‘ k âˆˆ Icc 0 âŒŠbâŒ‹â‚Š, f k * c k =
      f b * (âˆ‘ k âˆˆ Icc 0 âŒŠbâŒ‹â‚Š, c k) - âˆ« t in Set.Ioc 0 b, deriv f t * (âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k) := by
  nth_rewrite 1 [Finset.Icc_eq_cons_Ioc (Nat.zero_le _)]
  rw [sum_cons, â† Nat.floor_zero (Î± := â„), sum_mul_eq_sub_sub_integral_mul c le_rfl hb hf_diff
    hf_int, Nat.floor_zero, Nat.cast_zero, Icc_self, sum_singleton]
  ring

/-- Specialized version of `sum_mul_eq_sub_integral_mul` when the first coefficient of the sequence
`c` is equal to `0`. -/
theorem sum_mul_eq_sub_integral_mul' (hc : c 0 = 0) (b : â„)
    (hf_diff : âˆ€ t âˆˆ Set.Icc 1 b, DifferentiableAt â„ f t)
    (hf_int : IntegrableOn (deriv f) (Set.Icc 1 b)) :
    âˆ‘ k âˆˆ Icc 0 âŒŠbâŒ‹â‚Š, f k * c k =
      f b * (âˆ‘ k âˆˆ Icc 0 âŒŠbâŒ‹â‚Š, c k) - âˆ« t in Set.Ioc 1 b, deriv f t * (âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k) := by
  obtain hb | hb := le_or_gt 1 b
  Â· have : 1 â‰¤ âŒŠbâŒ‹â‚Š := (Nat.one_le_floor_iff _).mpr hb
    nth_rewrite 1 [Finset.Icc_eq_cons_Ioc (by linarith), sum_cons, â† Nat.Icc_succ_left,
      Finset.Icc_eq_cons_Ioc (by linarith), sum_cons]
    rw [Nat.succ_eq_add_one, zero_add, â† Nat.floor_one (Î± := â„),
      sum_mul_eq_sub_sub_integral_mul c zero_le_one hb hf_diff hf_int, Nat.floor_one, Nat.cast_one,
      Finset.Icc_eq_cons_Ioc zero_le_one, sum_cons, show 1 = 0 + 1 by rfl, Nat.Ioc_succ_singleton,
      zero_add, sum_singleton, hc, mul_zero, zero_add]
    ring
  Â· simp_rw [Nat.floor_eq_zero.mpr hb, Icc_self, sum_singleton, Nat.cast_zero, hc, mul_zero,
      Set.Ioc_eq_empty_of_le hb.le, Measure.restrict_empty, integral_zero_measure, sub_self]
