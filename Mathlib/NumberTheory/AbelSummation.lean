/-
Copyright (c) 2024 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.MeasureTheory.Function.Floor
import Mathlib.MeasureTheory.Integral.Asymptotics
import Mathlib.MeasureTheory.Integral.IntegralEqImproper
import Mathlib.Topology.Order.IsLocallyClosed

/-!
# Abel's summation formula

We prove several versions of Abel's summation formula.

## Results

* `sum_mul_eq_sub_sub_integral_mul`: general statement of the formula for a sum between two
  (nonnegative) reals `a` and `b`.

* `sum_mul_eq_sub_integral_mul`: a specialized version of `sum_mul_eq_sub_sub_integral_mul` for
  the case `a = 0`.

* `sum_mul_eq_sub_integral_mulâ‚€`: a specialized version of `sum_mul_eq_sub_integral_mul` for
  when the first coefficient of the sequence is `0`. This is useful for `ArithmeticFunction`.

Primed versions of the three results above are also stated for when the endpoints are `Nat`.

* `tendsto_sum_mul_atTop_nhds_one_sub_integral`: limit version of `sum_mul_eq_sub_integral_mul`
  when `a` tends to `âˆ`.

* `tendsto_sum_mul_atTop_nhds_one_sub_integralâ‚€`: limit version of `sum_mul_eq_sub_integral_mulâ‚€`
  when `a` tends to `âˆ`.

* `summable_mul_of_bigO_atTop`: let `c : â„• â†’ ğ•œ` and `f : â„ â†’ ğ•œ` with `ğ•œ = â„` or `â„‚`, prove the
  summability of `n â†¦ (c n) * (f n)` using Abel's formula under some `bigO` assumptions at infinity.

## References

* <https://en.wikipedia.org/wiki/Abel%27s_summation_formula>

-/

noncomputable section

open Finset MeasureTheory

variable {ğ•œ : Type*} [RCLike ğ•œ] (c : â„• â†’ ğ•œ) {f : â„ â†’ ğ•œ} {a b : â„}

namespace abelSummationProof

open intervalIntegral IntervalIntegrable

private theorem sumlocc {m : â„•} (n : â„•) :
    âˆ€áµ t, t âˆˆ Set.Icc (n : â„) (n + 1) â†’ âˆ‘ k âˆˆ Icc m âŒŠtâŒ‹â‚Š, c k = âˆ‘ k âˆˆ Icc m n, c k := by
  filter_upwards [Ico_ae_eq_Icc] with t h ht
  rw [Nat.floor_eq_on_Ico _ _ (h.mpr ht)]

open scoped Interval in
private theorem integralmulsum (hf_diff : âˆ€ t âˆˆ Set.Icc a b, DifferentiableAt â„ f t)
    (hf_int : IntegrableOn (deriv f) (Set.Icc a b)) (tâ‚ tâ‚‚ : â„) (n : â„•) (h : tâ‚ â‰¤ tâ‚‚)
    (hâ‚ : n â‰¤ tâ‚) (hâ‚‚ : tâ‚‚ â‰¤ n + 1) (hâ‚ƒ : a â‰¤ tâ‚) (hâ‚„ : tâ‚‚ â‰¤ b) :
    âˆ« t in tâ‚..tâ‚‚, deriv f t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k =
      (f tâ‚‚ - f tâ‚) * âˆ‘ k âˆˆ Icc 0 n, c k := by
  have h_incâ‚ : Î™ tâ‚ tâ‚‚ âŠ† Set.Icc n (n + 1) :=
    Set.uIoc_of_le h â–¸ Set.Ioc_subset_Icc_self.trans <| Set.Icc_subset_Icc hâ‚ hâ‚‚
  have h_incâ‚‚ : Set.uIcc tâ‚ tâ‚‚ âŠ† Set.Icc a b := Set.uIcc_of_le h â–¸ Set.Icc_subset_Icc hâ‚ƒ hâ‚„
  rw [â† integral_deriv_eq_sub (fun t ht â†¦ hf_diff t (h_incâ‚‚ ht)),
      â† intervalIntegral.integral_mul_const]
  Â· refine integral_congr_ae ?_
    filter_upwards [sumlocc c n] with t h h'
    rw [h (h_incâ‚ h')]
  Â· refine (intervalIntegrable_iff_integrableOn_Icc_of_le h).mpr (hf_int.mono_set ?_)
    rwa [â† Set.uIcc_of_le h]

private theorem ineqofmemIco {k : â„•} (hk : k âˆˆ Set.Ico (âŒŠaâŒ‹â‚Š + 1) âŒŠbâŒ‹â‚Š) :
    a â‰¤ k âˆ§ k + 1 â‰¤ b := by
  constructor
  Â· have := (Set.mem_Ico.mp hk).1
    exact le_of_lt <| (Nat.floor_lt' (by omega)).mp this
  Â· rw [â† Nat.cast_add_one, â† Nat.le_floor_iff' (Nat.succ_ne_zero k)]
    exact (Set.mem_Ico.mp hk).2

private theorem ineqofmemIco' {k : â„•} (hk : k âˆˆ Ico (âŒŠaâŒ‹â‚Š + 1) âŒŠbâŒ‹â‚Š) :
    a â‰¤ k âˆ§ k + 1 â‰¤ b :=
  ineqofmemIco (by rwa [â† Finset.coe_Ico])

theorem _root_.integrableOn_mul_sum_Icc {m : â„•} (ha : 0 â‰¤ a) {g : â„ â†’ ğ•œ}
    (hg_int : IntegrableOn g (Set.Icc a b)) :
    IntegrableOn (fun t â†¦ g t * âˆ‘ k âˆˆ Icc m âŒŠtâŒ‹â‚Š, c k) (Set.Icc a b) := by
  obtain hab | hab := le_or_gt a b
  Â· obtain hb | hb := eq_or_lt_of_le (Nat.floor_le_floor hab)
    Â· have : âˆ€áµ t, t âˆˆ Set.Icc a b â†’ âˆ‘ k âˆˆ Icc m âŒŠaâŒ‹â‚Š, c k = âˆ‘ k âˆˆ Icc m âŒŠtâŒ‹â‚Š, c k := by
        filter_upwards [sumlocc c âŒŠaâŒ‹â‚Š] with t htâ‚ htâ‚‚
        rw [htâ‚ âŸ¨(Nat.floor_le ha).trans htâ‚‚.1, hb â–¸ htâ‚‚.2.trans (Nat.lt_floor_add_one b).leâŸ©]
      rw [â† ae_restrict_iff' measurableSet_Icc] at this
      exact IntegrableOn.congr_fun_ae
        (hg_int.mul_const _) ((Filter.EventuallyEq.refl _ g).mul this)
    Â· have h_locint {tâ‚ tâ‚‚ : â„} {n : â„•} (h : tâ‚ â‰¤ tâ‚‚) (hâ‚ : n â‰¤ tâ‚) (hâ‚‚ : tâ‚‚ â‰¤ n + 1)
          (hâ‚ƒ : a â‰¤ tâ‚) (hâ‚„ : tâ‚‚ â‰¤ b) :
          IntervalIntegrable (fun t â†¦ g t * âˆ‘ k âˆˆ Icc m âŒŠtâŒ‹â‚Š, c k) volume tâ‚ tâ‚‚ := by
        rw [intervalIntegrable_iff_integrableOn_Icc_of_le h]
        exact (IntegrableOn.mono_set (hg_int.mul_const _) (Set.Icc_subset_Icc hâ‚ƒ hâ‚„)).congr
          <| ae_restrict_of_ae_restrict_of_subset (Set.Icc_subset_Icc hâ‚ hâ‚‚)
            <| (ae_restrict_iff' measurableSet_Icc).mpr
              (by filter_upwards [sumlocc c n] with t h ht using by rw [h ht])
      have aux1 : 0 â‰¤ b := (Nat.pos_of_floor_pos <| (Nat.zero_le _).trans_lt hb).le
      have aux2 : âŒŠaâŒ‹â‚Š + 1 â‰¤ b := by rwa [â† Nat.cast_add_one, â† Nat.le_floor_iff aux1]
      have aux3 : a â‰¤ âŒŠaâŒ‹â‚Š + 1 := (Nat.lt_floor_add_one _).le
      have aux4 : a â‰¤ âŒŠbâŒ‹â‚Š := le_of_lt (by rwa [â† Nat.floor_lt ha])
      -- now break up into 3 subintervals
      rw [â† intervalIntegrable_iff_integrableOn_Icc_of_le (aux3.trans aux2)]
      have I1 : IntervalIntegrable _ volume a â†‘(âŒŠaâŒ‹â‚Š + 1) :=
        h_locint (mod_cast aux3) (Nat.floor_le ha) (mod_cast le_rfl) le_rfl (mod_cast aux2)
      have I2 : IntervalIntegrable _ volume â†‘(âŒŠaâŒ‹â‚Š + 1) âŒŠbâŒ‹â‚Š :=
        trans_iterate_Ico hb fun k hk â†¦ h_locint (mod_cast k.le_succ)
          le_rfl (mod_cast le_rfl) (ineqofmemIco hk).1 (mod_cast (ineqofmemIco hk).2)
      have I3 : IntervalIntegrable _ volume âŒŠbâŒ‹â‚Š b :=
        h_locint (Nat.floor_le aux1) le_rfl (Nat.lt_floor_add_one _).le aux4 le_rfl
      exact (I1.trans I2).trans I3
  Â· rw [Set.Icc_eq_empty_of_lt hab]
    exact integrableOn_empty

/-- Abel's summation formula. -/
theorem _root_.sum_mul_eq_sub_sub_integral_mul (ha : 0 â‰¤ a) (hab : a â‰¤ b)
    (hf_diff : âˆ€ t âˆˆ Set.Icc a b, DifferentiableAt â„ f t)
    (hf_int : IntegrableOn (deriv f) (Set.Icc a b)) :
    âˆ‘ k âˆˆ Ioc âŒŠaâŒ‹â‚Š âŒŠbâŒ‹â‚Š, f k * c k =
      f b * (âˆ‘ k âˆˆ Icc 0 âŒŠbâŒ‹â‚Š, c k) - f a * (âˆ‘ k âˆˆ Icc 0 âŒŠaâŒ‹â‚Š, c k) -
        âˆ« t in Set.Ioc a b, deriv f t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k := by
  rw [â† integral_of_le hab]
  have aux1 : âŒŠaâŒ‹â‚Š â‰¤ a := Nat.floor_le ha
  have aux2 : b â‰¤ âŒŠbâŒ‹â‚Š + 1 := (Nat.lt_floor_add_one _).le
  -- We consider two cases depending on whether the sum is empty or not
  obtain hb | hb := eq_or_lt_of_le (Nat.floor_le_floor hab)
  Â· rw [hb, Ioc_eq_empty_of_le le_rfl, sum_empty, â† sub_mul,
      integralmulsum c hf_diff hf_int _ _ âŒŠbâŒ‹â‚Š hab (hb â–¸ aux1) aux2 le_rfl le_rfl, sub_self]
  have aux3 : a â‰¤ âŒŠaâŒ‹â‚Š + 1 := (Nat.lt_floor_add_one _).le
  have aux4 : âŒŠaâŒ‹â‚Š + 1 â‰¤ b := by rwa [â† Nat.cast_add_one, â† Nat.le_floor_iff (ha.trans hab)]
  have aux5 : âŒŠbâŒ‹â‚Š â‰¤ b := Nat.floor_le (ha.trans hab)
  have aux6 : a â‰¤ âŒŠbâŒ‹â‚Š := Nat.floor_lt ha |>.mp hb |>.le
  simp_rw [â† smul_eq_mul, sum_Ioc_by_parts (fun k â†¦ f k) _ hb, range_eq_Ico,
    Ico_add_one_right_eq_Icc, smul_eq_mul]
  have : âˆ‘ k âˆˆ Ioc âŒŠaâŒ‹â‚Š (âŒŠbâŒ‹â‚Š - 1), (f â†‘(k + 1) - f k) * âˆ‘ n âˆˆ Icc 0 k, c n =
        âˆ‘ k âˆˆ Ico (âŒŠaâŒ‹â‚Š + 1) âŒŠbâŒ‹â‚Š, âˆ« t in k..â†‘(k + 1), deriv f t * âˆ‘ n âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c n := by
    rw [â† Ico_add_one_add_one_eq_Ioc, Nat.sub_add_cancel (by omega), Eq.comm]
    exact sum_congr rfl fun k hk â†¦ (integralmulsum c hf_diff hf_int _ _ _  (mod_cast k.le_succ)
      le_rfl (mod_cast le_rfl) (ineqofmemIco' hk).1 <| mod_cast (ineqofmemIco' hk).2)
  rw [this, sum_integral_adjacent_intervals_Ico hb, Nat.cast_add, Nat.cast_one,
    â† integral_interval_sub_left (a := a) (c := âŒŠaâŒ‹â‚Š + 1),
    â† integral_add_adjacent_intervals (b := âŒŠbâŒ‹â‚Š) (c := b),
    integralmulsum c hf_diff hf_int _ _ _ aux3 aux1 le_rfl le_rfl aux4,
    integralmulsum c hf_diff hf_int _ _ _ aux5 le_rfl aux2 aux6 le_rfl]
  Â· ring
  -- now deal with the integrability side goals
  -- (Note we have 5 goals, but the 1st and 3rd are identical. TODO: find a non-hacky way of dealing
  -- with both at once.)
  Â· rw [intervalIntegrable_iff_integrableOn_Icc_of_le aux6]
    exact (integrableOn_mul_sum_Icc c ha hf_int).mono_set (Set.Icc_subset_Icc_right aux5)
  Â· rw [intervalIntegrable_iff_integrableOn_Icc_of_le aux5]
    exact (integrableOn_mul_sum_Icc c ha hf_int).mono_set (Set.Icc_subset_Icc_left aux6)
  Â· rw [intervalIntegrable_iff_integrableOn_Icc_of_le aux6]
    exact (integrableOn_mul_sum_Icc c ha hf_int).mono_set (Set.Icc_subset_Icc_right aux5)
  Â· rw [intervalIntegrable_iff_integrableOn_Icc_of_le aux3]
    exact (integrableOn_mul_sum_Icc c ha hf_int).mono_set (Set.Icc_subset_Icc_right aux4)
  Â· exact fun k hk â†¦ (intervalIntegrable_iff_integrableOn_Icc_of_le (mod_cast k.le_succ)).mpr
      <| (integrableOn_mul_sum_Icc c ha hf_int).mono_set
        <| (Set.Icc_subset_Icc_iff (mod_cast k.le_succ)).mpr <| mod_cast (ineqofmemIco hk)

/-- A version of `sum_mul_eq_sub_sub_integral_mul` where the endpoints are `Nat`. -/
theorem _root_.sum_mul_eq_sub_sub_integral_mul' {n m : â„•} (h : n â‰¤ m)
    (hf_diff : âˆ€ t âˆˆ Set.Icc (n : â„) m, DifferentiableAt â„ f t)
    (hf_int : IntegrableOn (deriv f) (Set.Icc (n : â„) m)) :
    âˆ‘ k âˆˆ Ioc n m, f k * c k =
      f m * (âˆ‘ k âˆˆ Icc 0 m, c k) - f n * (âˆ‘ k âˆˆ Icc 0 n, c k) -
        âˆ« t in Set.Ioc (n : â„) m, deriv f t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k := by
  convert sum_mul_eq_sub_sub_integral_mul c n.cast_nonneg (Nat.cast_le.mpr h) hf_diff hf_int
  all_goals rw [Nat.floor_natCast]

end abelSummationProof

section specialversions

/-- Specialized version of `sum_mul_eq_sub_sub_integral_mul` for the case `a = 0` -/
theorem sum_mul_eq_sub_integral_mul {b : â„} (hb : 0 â‰¤ b)
    (hf_diff : âˆ€ t âˆˆ Set.Icc 0 b, DifferentiableAt â„ f t)
    (hf_int : IntegrableOn (deriv f) (Set.Icc 0 b)) :
    âˆ‘ k âˆˆ Icc 0 âŒŠbâŒ‹â‚Š, f k * c k =
      f b * (âˆ‘ k âˆˆ Icc 0 âŒŠbâŒ‹â‚Š, c k) - âˆ« t in Set.Ioc 0 b, deriv f t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k := by
  nth_rewrite 1 [Icc_eq_cons_Ioc (Nat.zero_le _)]
  rw [sum_cons, â† Nat.floor_zero (R := â„), sum_mul_eq_sub_sub_integral_mul c le_rfl hb hf_diff
    hf_int, Nat.floor_zero, Nat.cast_zero, Icc_self, sum_singleton]
  ring

/-- A version of `sum_mul_eq_sub_integral_mul` where the endpoint is a `Nat`. -/
theorem sum_mul_eq_sub_integral_mul' (m : â„•)
    (hf_diff : âˆ€ t âˆˆ Set.Icc (0 : â„) m, DifferentiableAt â„ f t)
    (hf_int : IntegrableOn (deriv f) (Set.Icc (0 : â„) m)) :
    âˆ‘ k âˆˆ Icc 0 m, f k * c k =
      f m * (âˆ‘ k âˆˆ Icc 0 m, c k) -
        âˆ« t in Set.Ioc (0 : â„) m, deriv f t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k := by
  convert sum_mul_eq_sub_integral_mul c m.cast_nonneg hf_diff hf_int
  all_goals rw [Nat.floor_natCast]

/-- Specialized version of `sum_mul_eq_sub_integral_mul` when the first coefficient of the sequence
`c` is equal to `0`. -/
theorem sum_mul_eq_sub_integral_mulâ‚€ (hc : c 0 = 0) (b : â„)
    (hf_diff : âˆ€ t âˆˆ Set.Icc 1 b, DifferentiableAt â„ f t)
    (hf_int : IntegrableOn (deriv f) (Set.Icc 1 b)) :
    âˆ‘ k âˆˆ Icc 0 âŒŠbâŒ‹â‚Š, f k * c k =
      f b * (âˆ‘ k âˆˆ Icc 0 âŒŠbâŒ‹â‚Š, c k) - âˆ« t in Set.Ioc 1 b, deriv f t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k := by
  obtain hb | hb := le_or_gt 1 b
  Â· have : 1 â‰¤ âŒŠbâŒ‹â‚Š := (Nat.one_le_floor_iff _).mpr hb
    nth_rewrite 1 [Icc_eq_cons_Ioc (Nat.zero_le _), sum_cons, â† Icc_add_one_left_eq_Ioc,
      Icc_eq_cons_Ioc (by omega), sum_cons]
    rw [zero_add, â† Nat.floor_one (R := â„),
      sum_mul_eq_sub_sub_integral_mul c zero_le_one hb hf_diff hf_int, Nat.floor_one, Nat.cast_one,
      Icc_eq_cons_Ioc zero_le_one, sum_cons, show 1 = 0 + 1 by rfl, Nat.Ioc_succ_singleton,
      zero_add, sum_singleton, hc, mul_zero, zero_add]
    ring
  Â· simp_rw [Nat.floor_eq_zero.mpr hb, Icc_self, sum_singleton, Nat.cast_zero, hc, mul_zero,
      Set.Ioc_eq_empty_of_le hb.le, Measure.restrict_empty, integral_zero_measure, sub_self]

/-- A version of `sum_mul_eq_sub_integral_mulâ‚€` where the endpoint is a `Nat`. -/
theorem sum_mul_eq_sub_integral_mulâ‚€' (hc : c 0 = 0) (m : â„•)
    (hf_diff : âˆ€ t âˆˆ Set.Icc (1 : â„) m, DifferentiableAt â„ f t)
    (hf_int : IntegrableOn (deriv f) (Set.Icc (1 : â„) m)) :
    âˆ‘ k âˆˆ Icc 0 m, f k * c k =
      f m * (âˆ‘ k âˆˆ Icc 0 m, c k) -
        âˆ« t in Set.Ioc (1 : â„) m, deriv f t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k := by
  convert sum_mul_eq_sub_integral_mulâ‚€ c hc m hf_diff hf_int
  all_goals rw [Nat.floor_natCast]

end specialversions

section limit

open Filter Topology abelSummationProof intervalIntegral

theorem locallyIntegrableOn_mul_sum_Icc {m : â„•} (ha : 0 â‰¤ a) {g : â„ â†’ ğ•œ}
    (hg : LocallyIntegrableOn g (Set.Ici a)) :
    LocallyIntegrableOn (fun t â†¦ g t * âˆ‘ k âˆˆ Icc m âŒŠtâŒ‹â‚Š, c k) (Set.Ici a) := by
  refine (locallyIntegrableOn_iff isLocallyClosed_Ici).mpr fun K hKâ‚ hKâ‚‚ â†¦ ?_
  by_cases hKâ‚ƒ : K.Nonempty
  Â· have h_inf : a â‰¤ sInf K := (hKâ‚ (hKâ‚‚.sInf_mem hKâ‚ƒ))
    refine IntegrableOn.mono_set ?_ (Bornology.IsBounded.subset_Icc_sInf_sSup hKâ‚‚.isBounded)
    refine integrableOn_mul_sum_Icc _ (ha.trans h_inf) ?_
    refine hg.integrableOn_compact_subset ?_ isCompact_Icc
    exact (Set.Icc_subset_Ici_iff (Real.sInf_le_sSup _ hKâ‚‚.bddBelow hKâ‚‚.bddAbove)).mpr h_inf
  Â· rw [Set.not_nonempty_iff_eq_empty.mp hKâ‚ƒ]
    exact integrableOn_empty

theorem tendsto_sum_mul_atTop_nhds_one_sub_integral
    (hf_diff : âˆ€ t âˆˆ Set.Ici 0, DifferentiableAt â„ f t)
    (hf_int : LocallyIntegrableOn (deriv f) (Set.Ici 0)) {l : ğ•œ}
    (h_lim : Tendsto (fun n : â„• â†¦ f n * âˆ‘ k âˆˆ Icc 0 n, c k) atTop (ğ“ l))
    {g : â„ â†’ ğ•œ} (hg_dom : (fun t â†¦ deriv f t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k) =O[atTop] g)
    (hg_int : IntegrableAtFilter g atTop) :
    Tendsto (fun n : â„• â†¦ âˆ‘ k âˆˆ Icc 0 n, f k * c k) atTop
      (ğ“ (l - âˆ« t in Set.Ioi 0, deriv f t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k)) := by
  have h_lim' : Tendsto (fun n : â„• â†¦ âˆ« t in Set.Ioc (0 : â„) n, deriv f t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k)
      atTop (ğ“ (âˆ« t in Set.Ioi 0, deriv f t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k)) := by
    refine Tendsto.congr (fun _ â†¦ by rw [â† integral_of_le (Nat.cast_nonneg _)]) ?_
    refine intervalIntegral_tendsto_integral_Ioi _ ?_ tendsto_natCast_atTop_atTop
    exact integrableOn_Ici_iff_integrableOn_Ioi.mp
      <| (locallyIntegrableOn_mul_sum_Icc c le_rfl hf_int).integrableOn_of_isBigO_atTop
        hg_dom hg_int
  refine (h_lim.sub h_lim').congr (fun _ â†¦ ?_)
  rw [sum_mul_eq_sub_integral_mul' _ _ (fun t ht â†¦ hf_diff _ ht.1)]
  exact hf_int.integrableOn_compact_subset Set.Icc_subset_Ici_self isCompact_Icc

theorem tendsto_sum_mul_atTop_nhds_one_sub_integralâ‚€ (hc : c 0 = 0)
    (hf_diff : âˆ€ t âˆˆ Set.Ici 1, DifferentiableAt â„ f t)
    (hf_int : LocallyIntegrableOn (deriv f) (Set.Ici 1)) {l : ğ•œ}
    (h_lim : Tendsto (fun n : â„• â†¦ f n * âˆ‘ k âˆˆ Icc 0 n, c k) atTop (ğ“ l))
    {g : â„ â†’ â„} (hg_dom : (fun t â†¦ deriv f t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k) =O[atTop] g)
    (hg_int : IntegrableAtFilter g atTop) :
    Tendsto (fun n : â„• â†¦ âˆ‘ k âˆˆ Icc 0 n, f k * c k) atTop
      (ğ“ (l - âˆ« t in Set.Ioi 1, deriv f t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k)) := by
  have h : (fun n : â„• â†¦ âˆ« (x : â„) in (1 : â„)..n, deriv f x * âˆ‘ k âˆˆ Icc 0 âŒŠxâŒ‹â‚Š, c k) =á¶ [atTop]
      (fun n : â„• â†¦ âˆ« (t : â„) in Set.Ioc 1 â†‘n, deriv f t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k) := by
    filter_upwards [eventually_ge_atTop 1] with _ h
    rw [â† integral_of_le (Nat.one_le_cast.mpr h)]
  have h_lim' : Tendsto (fun n : â„• â†¦ âˆ« t in Set.Ioc (1 : â„) n, deriv f t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k)
      atTop (ğ“ (âˆ« t in Set.Ioi 1, deriv f t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, c k)) := by
    refine Tendsto.congr' h (intervalIntegral_tendsto_integral_Ioi _ ?_ tendsto_natCast_atTop_atTop)
    exact integrableOn_Ici_iff_integrableOn_Ioi.mp
      <| (locallyIntegrableOn_mul_sum_Icc c zero_le_one hf_int).integrableOn_of_isBigO_atTop
        hg_dom hg_int
  refine (h_lim.sub h_lim').congr (fun _ â†¦ ?_)
  rw [sum_mul_eq_sub_integral_mulâ‚€' _ hc _ (fun t ht â†¦ hf_diff _ ht.1)]
  exact hf_int.integrableOn_compact_subset Set.Icc_subset_Ici_self isCompact_Icc

end limit

section summable

open Filter abelSummationProof

private theorem summable_mul_of_bigO_atTop_aux (m : â„•)
    (h_bdd : (fun n : â„• â†¦ â€–f nâ€– * âˆ‘ k âˆˆ Icc 0 n, â€–c kâ€–) =O[atTop] fun _ â†¦ (1 : â„))
    (hf_int : LocallyIntegrableOn (deriv (fun t â†¦ â€–f tâ€–)) (Set.Ici (m : â„)))
    (hf : âˆ€ n : â„•, âˆ‘ k âˆˆ Icc 0 n, â€–f kâ€– * â€–c kâ€– =
      â€–f nâ€– * âˆ‘ k âˆˆ Icc 0 n, â€–c kâ€– -
        âˆ« (t : â„) in Set.Ioc â†‘m â†‘n, deriv (fun t â†¦ â€–f tâ€–) t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, â€–c kâ€–)
    {g : â„ â†’ â„}
    (hgâ‚ : (fun t â†¦ deriv (fun t â†¦ â€–f tâ€–) t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, â€–c kâ€–) =O[atTop] g)
    (hgâ‚‚ : IntegrableAtFilter g atTop) :
    Summable (fun n : â„• â†¦ f n * c n) := by
  obtain âŸ¨Câ‚, hCâ‚âŸ© := Asymptotics.isBigO_one_nat_atTop_iff.mp h_bdd
  let Câ‚‚ := âˆ« t in Set.Ioi (m : â„), â€–deriv (fun t â†¦ â€–f tâ€–) t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, â€–c kâ€–â€–
  refine summable_of_sum_range_norm_le (c := max (Câ‚ + Câ‚‚) 1) fun n â†¦ ?_
  cases n with
  | zero => simp only [range_zero, norm_mul, sum_empty, le_sup_iff, zero_le_one, or_true]
  | succ n =>
      have h_mes : Measurable fun t â†¦ deriv (fun t â†¦ â€–f tâ€–) t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, â€–c kâ€– :=
        (measurable_deriv _).mul <| Measurable.comp' (g := fun n : â„• â†¦ âˆ‘ k âˆˆ Icc 0 n, â€–c kâ€–)
          (fun _ _ â†¦ trivial) Nat.measurable_floor
      rw [Nat.range_eq_Icc_zero_sub_one _ n.add_one_ne_zero, add_tsub_cancel_right]
      calc
        _ = âˆ‘ k âˆˆ Icc 0 n, â€–f kâ€– * â€–c kâ€– := by simp_rw [norm_mul]
        _ = â€–f nâ€– * âˆ‘ k âˆˆ Icc 0 n, â€–c kâ€– -
              âˆ« t in Set.Ioc â†‘m â†‘n, deriv (fun t â†¦ â€–f tâ€–) t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, â€–c kâ€– := ?_
        _ â‰¤ Câ‚ - âˆ« t in Set.Ioc â†‘m â†‘n, deriv (fun t â†¦ â€–f tâ€–) t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, â€–c kâ€– := ?_
        _ â‰¤ Câ‚ + âˆ« t in Set.Ioc â†‘m â†‘n, â€–deriv (fun t â†¦ â€–f tâ€–) t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, â€–c kâ€–â€– := ?_
        _ â‰¤ Câ‚ + Câ‚‚ := ?_
        _ â‰¤ max (Câ‚ + Câ‚‚) 1 := le_max_left _ _
      Â· exact hf _
      Â· refine tsub_le_tsub_right (le_of_eq_of_le (Real.norm_of_nonneg ?_).symm (hCâ‚ n)) _
        exact mul_nonneg (norm_nonneg _) (sum_nonneg fun _ _ â†¦ norm_nonneg _)
      Â· exact add_le_add_left
          (le_trans (neg_le_abs _) (Real.norm_eq_abs _ â–¸ norm_integral_le_integral_norm _)) _
      Â· refine add_le_add_left (setIntegral_mono_set ?_ ?_ Set.Ioc_subset_Ioi_self.eventuallyLE) Câ‚
        Â· exact integrableOn_Ici_iff_integrableOn_Ioi.mp <|
            (integrable_norm_iff h_mes.aestronglyMeasurable).mpr <|
              (locallyIntegrableOn_mul_sum_Icc _ m.cast_nonneg hf_int).integrableOn_of_isBigO_atTop
                hgâ‚ hgâ‚‚
        Â· filter_upwards with t using norm_nonneg _

theorem summable_mul_of_bigO_atTop
    (hf_diff : âˆ€ t âˆˆ Set.Ici 0, DifferentiableAt â„ (fun x â†¦ â€–f xâ€–) t)
    (hf_int : LocallyIntegrableOn (deriv (fun t â†¦ â€–f tâ€–)) (Set.Ici 0))
    (h_bdd : (fun n : â„• â†¦ â€–f nâ€– * âˆ‘ k âˆˆ Icc 0 n, â€–c kâ€–) =O[atTop] fun _ â†¦ (1 : â„))
    {g : â„ â†’ â„} (hgâ‚ : (fun t â†¦ deriv (fun t â†¦ â€–f tâ€–) t * âˆ‘ k âˆˆ Icc 0 âŒŠtâŒ‹â‚Š, â€–c kâ€–) =O[atTop] g)
    (hgâ‚‚ : IntegrableAtFilter g atTop) :
    Summable (fun n : â„• â†¦ f n * c n) := by
  refine summable_mul_of_bigO_atTop_aux c 0 h_bdd (by rwa [Nat.cast_zero]) (fun n â†¦ ?_) hgâ‚ hgâ‚‚
  exact_mod_cast sum_mul_eq_sub_integral_mul' _ _ (fun _ ht â†¦ hf_diff _ ht.1)
    (hf_int.integrableOn_compact_subset Set.Icc_subset_Ici_self isCompact_Icc)

/-- A version of `summable_mul_of_bigO_atTop` that can be useful to avoid difficulties near zero. -/
theorem summable_mul_of_bigO_atTop'
    (hf_diff : âˆ€ t âˆˆ Set.Ici 1, DifferentiableAt â„ (fun x â†¦ â€–f xâ€–) t)
    (hf_int : LocallyIntegrableOn (deriv (fun t â†¦ â€–f tâ€–)) (Set.Ici 1))
    (h_bdd : (fun n : â„• â†¦ â€–f nâ€– * âˆ‘ k âˆˆ Icc 1 n, â€–c kâ€–) =O[atTop] fun _ â†¦ (1 : â„))
    {g : â„ â†’ â„} (hgâ‚ : (fun t â†¦ deriv (fun t â†¦ â€–f tâ€–) t * âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, â€–c kâ€–) =O[atTop] g)
    (hgâ‚‚ : IntegrableAtFilter g atTop) :
    Summable (fun n : â„• â†¦ f n * c n) := by
  have h : âˆ€ n, âˆ‘ k âˆˆ Icc 1 n, â€–c kâ€– = âˆ‘ k âˆˆ Icc 0 n, â€–(fun n â†¦ if n = 0 then 0 else c n) kâ€– := by
    intro n
    rw [Icc_eq_cons_Ioc n.zero_le, sum_cons, â† Icc_add_one_left_eq_Ioc, zero_add]
    simp_rw [if_pos, norm_zero, zero_add]
    exact Finset.sum_congr rfl fun _ h â†¦ by rw [if_neg (zero_lt_one.trans_le (mem_Icc.mp h).1).ne']
  simp_rw [h] at h_bdd hgâ‚
  refine Summable.congr_atTop (summable_mul_of_bigO_atTop_aux (fun n â†¦ if n = 0 then 0 else c n) 1
    h_bdd (by rwa [Nat.cast_one]) (fun n â†¦ ?_) hgâ‚ hgâ‚‚) ?_
  Â· exact_mod_cast sum_mul_eq_sub_integral_mulâ‚€' _ (by simp only [reduceIte, norm_zero]) n
      (fun _ ht â†¦ hf_diff _ ht.1)
      (hf_int.integrableOn_compact_subset Set.Icc_subset_Ici_self isCompact_Icc)
  Â· filter_upwards [eventually_ne_atTop 0] with k hk
    simp_rw [if_neg hk]

end summable
