/-
Copyright (c) 2022 Anne Baanen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anne Baanen
-/
import Mathlib.LinearAlgebra.FreeModule.Finite.Rank
import Mathlib.RingTheory.DedekindDomain.Ideal

#align_import number_theory.ramification_inertia from "leanprover-community/mathlib"@"039a089d2a4b93c761b234f3e5f5aeb752bac60f"

/-!
# Ramification index and inertia degree

Given `P : Ideal S` lying over `p : Ideal R` for the ring extension `f : R ‚Üí+* S`
(assuming `P` and `p` are prime or maximal where needed),
the **ramification index** `Ideal.ramificationIdx f p P` is the multiplicity of `P` in `map f p`,
and the **inertia degree** `Ideal.inertiaDeg f p P` is the degree of the field extension
`(S / P) : (R / p)`.

## Main results

The main theorem `Ideal.sum_ramification_inertia` states that for all coprime `P` lying over `p`,
`Œ£ P, ramification_idx f p P * inertia_deg f p P` equals the degree of the field extension
`Frac(S) : Frac(R)`.

## Implementation notes

Often the above theory is set up in the case where:
 * `R` is the ring of integers of a number field `K`,
 * `L` is a finite separable extension of `K`,
 * `S` is the integral closure of `R` in `L`,
 * `p` and `P` are maximal ideals,
 * `P` is an ideal lying over `p`
We will try to relax the above hypotheses as much as possible.

## Notation

In this file, `e` stands for the ramification index and `f` for the inertia degree of `P` over `p`,
leaving `p` and `P` implicit.

-/


namespace Ideal

universe u v

variable {R : Type u} [CommRing R]

variable {S : Type v} [CommRing S] (f : R ‚Üí+* S)

variable (p : Ideal R) (P : Ideal S)

open FiniteDimensional

open UniqueFactorizationMonoid

section DecEq

open scoped Classical

/-- The ramification index of `P` over `p` is the largest exponent `n` such that
`p` is contained in `P^n`.

In particular, if `p` is not contained in `P^n`, then the ramification index is 0.

If there is no largest such `n` (e.g. because `p = ‚ä•`), then `ramificationIdx` is
defined to be 0.
-/
noncomputable def ramificationIdx : ‚Ñï := sSup {n | map f p ‚â§ P ^ n}
#align ideal.ramification_idx Ideal.ramificationIdx

variable {f p P}

theorem ramificationIdx_eq_find (h : ‚àÉ n, ‚àÄ k, map f p ‚â§ P ^ k ‚Üí k ‚â§ n) :
    ramificationIdx f p P = Nat.find h :=
  Nat.sSup_def h
#align ideal.ramification_idx_eq_find Ideal.ramificationIdx_eq_find

theorem ramificationIdx_eq_zero (h : ‚àÄ n : ‚Ñï, ‚àÉ k, map f p ‚â§ P ^ k ‚àß n < k) :
    ramificationIdx f p P = 0 :=
  dif_neg (by push_neg; exact h)
              -- ‚ä¢ ‚àÄ (n : ‚Ñï), ‚àÉ a, a ‚àà {n | map f p ‚â§ P ^ n} ‚àß n < a
                        -- üéâ no goals
#align ideal.ramification_idx_eq_zero Ideal.ramificationIdx_eq_zero

theorem ramificationIdx_spec {n : ‚Ñï} (hle : map f p ‚â§ P ^ n) (hgt : ¬¨map f p ‚â§ P ^ (n + 1)) :
    ramificationIdx f p P = n := by
  let Q : ‚Ñï ‚Üí Prop := fun m => ‚àÄ k : ‚Ñï, map f p ‚â§ P ^ k ‚Üí k ‚â§ m
  -- ‚ä¢ ramificationIdx f p P = n
  have : Q n := by
    intro k hk
    refine le_of_not_lt fun hnk => ?_
    exact hgt (hk.trans (Ideal.pow_le_pow hnk))
  rw [ramificationIdx_eq_find ‚ü®n, this‚ü©]
  -- ‚ä¢ Nat.find (_ : ‚àÉ n, ‚àÄ (k : ‚Ñï), map f p ‚â§ P ^ k ‚Üí k ‚â§ n) = n
  refine le_antisymm (Nat.find_min' _ this) (le_of_not_gt fun h : Nat.find _ < n => ?_)
  -- ‚ä¢ False
  obtain this' := Nat.find_spec ‚ü®n, this‚ü©
  -- ‚ä¢ False
  exact h.not_le (this' _ hle)
  -- üéâ no goals
#align ideal.ramification_idx_spec Ideal.ramificationIdx_spec

theorem ramificationIdx_lt {n : ‚Ñï} (hgt : ¬¨map f p ‚â§ P ^ n) : ramificationIdx f p P < n := by
  cases' n with n n
  -- ‚ä¢ ramificationIdx f p P < Nat.zero
  ¬∑ simp at hgt
    -- üéâ no goals
  ¬∑ rw [Nat.lt_succ_iff]
    -- ‚ä¢ ramificationIdx f p P ‚â§ n
    have : ‚àÄ k, map f p ‚â§ P ^ k ‚Üí k ‚â§ n := by
      refine fun k hk => le_of_not_lt fun hnk => ?_
      exact hgt (hk.trans (Ideal.pow_le_pow hnk))
    rw [ramificationIdx_eq_find ‚ü®n, this‚ü©]
    -- ‚ä¢ Nat.find (_ : ‚àÉ n, ‚àÄ (k : ‚Ñï), map f p ‚â§ P ^ k ‚Üí k ‚â§ n) ‚â§ n
    exact Nat.find_min' ‚ü®n, this‚ü© this
    -- üéâ no goals
#align ideal.ramification_idx_lt Ideal.ramificationIdx_lt

@[simp]
theorem ramificationIdx_bot : ramificationIdx f ‚ä• P = 0 :=
  dif_neg <| not_exists.mpr fun n hn => n.lt_succ_self.not_le (hn _ (by simp))
                                                                        -- üéâ no goals
#align ideal.ramification_idx_bot Ideal.ramificationIdx_bot

@[simp]
theorem ramificationIdx_of_not_le (h : ¬¨map f p ‚â§ P) : ramificationIdx f p P = 0 :=
  ramificationIdx_spec (by simp) (by simpa using h)
                           -- üéâ no goals
                                     -- üéâ no goals
#align ideal.ramification_idx_of_not_le Ideal.ramificationIdx_of_not_le

theorem ramificationIdx_ne_zero {e : ‚Ñï} (he : e ‚â† 0) (hle : map f p ‚â§ P ^ e)
    (hnle : ¬¨map f p ‚â§ P ^ (e + 1)) : ramificationIdx f p P ‚â† 0 := by
  rwa [ramificationIdx_spec hle hnle]
  -- üéâ no goals
#align ideal.ramification_idx_ne_zero Ideal.ramificationIdx_ne_zero

theorem le_pow_of_le_ramificationIdx {n : ‚Ñï} (hn : n ‚â§ ramificationIdx f p P) :
    map f p ‚â§ P ^ n := by
  contrapose! hn
  -- ‚ä¢ ramificationIdx f p P < n
  exact ramificationIdx_lt hn
  -- üéâ no goals
#align ideal.le_pow_of_le_ramification_idx Ideal.le_pow_of_le_ramificationIdx

theorem le_pow_ramificationIdx : map f p ‚â§ P ^ ramificationIdx f p P :=
  le_pow_of_le_ramificationIdx (le_refl _)
#align ideal.le_pow_ramification_idx Ideal.le_pow_ramificationIdx

theorem le_comap_pow_ramificationIdx : p ‚â§ comap f (P ^ ramificationIdx f p P) :=
  map_le_iff_le_comap.mp le_pow_ramificationIdx
#align ideal.le_comap_pow_ramification_idx Ideal.le_comap_pow_ramificationIdx

theorem le_comap_of_ramificationIdx_ne_zero (h : ramificationIdx f p P ‚â† 0) : p ‚â§ comap f P :=
  Ideal.map_le_iff_le_comap.mp <| le_pow_ramificationIdx.trans <| Ideal.pow_le_self <| h
#align ideal.le_comap_of_ramification_idx_ne_zero Ideal.le_comap_of_ramificationIdx_ne_zero

namespace IsDedekindDomain

variable [IsDomain S] [IsDedekindDomain S]

theorem ramificationIdx_eq_normalizedFactors_count (hp0 : map f p ‚â† ‚ä•) (hP : P.IsPrime)
    (hP0 : P ‚â† ‚ä•) : ramificationIdx f p P = (normalizedFactors (map f p)).count P := by
  have hPirr := (Ideal.prime_of_isPrime hP0 hP).irreducible
  -- ‚ä¢ ramificationIdx f p P = Multiset.count P (normalizedFactors (map f p))
  refine ramificationIdx_spec (Ideal.le_of_dvd ?_) (mt Ideal.dvd_iff_le.mpr ?_) <;>
  -- ‚ä¢ P ^ Multiset.count P (normalizedFactors (map f p)) ‚à£ map f p
    rw [dvd_iff_normalizedFactors_le_normalizedFactors (pow_ne_zero _ hP0) hp0,
      normalizedFactors_pow, normalizedFactors_irreducible hPirr, normalize_eq,
      Multiset.nsmul_singleton, ‚Üê Multiset.le_count_iff_replicate_le]
  ¬∑ exact (Nat.lt_succ_self _).not_le
    -- üéâ no goals
#align ideal.is_dedekind_domain.ramification_idx_eq_normalized_factors_count Ideal.IsDedekindDomain.ramificationIdx_eq_normalizedFactors_count

theorem ramificationIdx_eq_factors_count (hp0 : map f p ‚â† ‚ä•) (hP : P.IsPrime) (hP0 : P ‚â† ‚ä•) :
    ramificationIdx f p P = (factors (map f p)).count P := by
  rw [IsDedekindDomain.ramificationIdx_eq_normalizedFactors_count hp0 hP hP0,
    factors_eq_normalizedFactors]
#align ideal.is_dedekind_domain.ramification_idx_eq_factors_count Ideal.IsDedekindDomain.ramificationIdx_eq_factors_count

theorem ramificationIdx_ne_zero (hp0 : map f p ‚â† ‚ä•) (hP : P.IsPrime) (le : map f p ‚â§ P) :
    ramificationIdx f p P ‚â† 0 := by
  have hP0 : P ‚â† ‚ä• := by
    rintro rfl
    have := le_bot_iff.mp le
    contradiction
  have hPirr := (Ideal.prime_of_isPrime hP0 hP).irreducible
  -- ‚ä¢ ramificationIdx f p P ‚â† 0
  rw [IsDedekindDomain.ramificationIdx_eq_normalizedFactors_count hp0 hP hP0]
  -- ‚ä¢ Multiset.count P (normalizedFactors (map f p)) ‚â† 0
  obtain ‚ü®P', hP', P'_eq‚ü© :=
    exists_mem_normalizedFactors_of_dvd hp0 hPirr (Ideal.dvd_iff_le.mpr le)
  rwa [Multiset.count_ne_zero, associated_iff_eq.mp P'_eq]
  -- üéâ no goals
#align ideal.is_dedekind_domain.ramification_idx_ne_zero Ideal.IsDedekindDomain.ramificationIdx_ne_zero

end IsDedekindDomain

variable (f p P)

attribute [local instance] Ideal.Quotient.field

/-- The inertia degree of `P : Ideal S` lying over `p : Ideal R` is the degree of the
extension `(S / P) : (R / p)`.

We do not assume `P` lies over `p` in the definition; we return `0` instead.

See `inertiaDeg_algebraMap` for the common case where `f = algebraMap R S`
and there is an algebra structure `R / p ‚Üí S / P`.
-/
noncomputable def inertiaDeg [p.IsMaximal] : ‚Ñï :=
  if hPp : comap f P = p then
    @finrank (R ‚ß∏ p) (S ‚ß∏ P) _ _ <|
      @Algebra.toModule _ _ _ _ <|
        RingHom.toAlgebra <|
          Ideal.Quotient.lift p ((Ideal.Quotient.mk P).comp f) fun _ ha =>
            Quotient.eq_zero_iff_mem.mpr <| mem_comap.mp <| hPp.symm ‚ñ∏ ha
  else 0
#align ideal.inertia_deg Ideal.inertiaDeg

-- Useful for the `nontriviality` tactic using `comap_eq_of_scalar_tower_quotient`.
@[simp]
theorem inertiaDeg_of_subsingleton [hp : p.IsMaximal] [hQ : Subsingleton (S ‚ß∏ P)] :
    inertiaDeg f p P = 0 := by
  have := Ideal.Quotient.subsingleton_iff.mp hQ
  -- ‚ä¢ inertiaDeg f p P = 0
  subst this
  -- ‚ä¢ inertiaDeg f p ‚ä§ = 0
  exact dif_neg fun h => hp.ne_top <| h.symm.trans comap_top
  -- üéâ no goals
#align ideal.inertia_deg_of_subsingleton Ideal.inertiaDeg_of_subsingleton

@[simp]
theorem inertiaDeg_algebraMap [Algebra R S] [Algebra (R ‚ß∏ p) (S ‚ß∏ P)]
    [IsScalarTower R (R ‚ß∏ p) (S ‚ß∏ P)] [hp : p.IsMaximal] :
    inertiaDeg (algebraMap R S) p P = finrank (R ‚ß∏ p) (S ‚ß∏ P) := by
  nontriviality S ‚ß∏ P using inertiaDeg_of_subsingleton, finrank_zero_of_subsingleton
  -- ‚ä¢ inertiaDeg (algebraMap R S) p P = finrank (R ‚ß∏ p) (S ‚ß∏ P)
  have := comap_eq_of_scalar_tower_quotient (algebraMap (R ‚ß∏ p) (S ‚ß∏ P)).injective
  -- ‚ä¢ inertiaDeg (algebraMap R S) p P = finrank (R ‚ß∏ p) (S ‚ß∏ P)
  rw [inertiaDeg, dif_pos this]
  -- ‚ä¢ finrank (R ‚ß∏ p) (S ‚ß∏ P) = finrank (R ‚ß∏ p) (S ‚ß∏ P)
  congr
  -- ‚ä¢ RingHom.toAlgebra (Quotient.lift p (RingHom.comp (Quotient.mk P) (algebraMap ‚Ä¶
  refine' Algebra.algebra_ext _ _ fun x' => Quotient.inductionOn' x' fun x => _
  -- ‚ä¢ ‚Üë(algebraMap (R ‚ß∏ p) (S ‚ß∏ P)) (Quotient.mk'' x) = ‚Üë(algebraMap (R ‚ß∏ p) (S ‚ß∏  ‚Ä¶
  change Ideal.Quotient.lift p _ _ (Ideal.Quotient.mk p x) = algebraMap _ _ (Ideal.Quotient.mk p x)
  -- ‚ä¢ ‚Üë(Quotient.lift p (RingHom.comp (Quotient.mk P) (algebraMap R S)) (_ : ‚àÄ (x  ‚Ä¶
  rw [Ideal.Quotient.lift_mk, ‚Üê Ideal.Quotient.algebraMap_eq P, ‚Üê IsScalarTower.algebraMap_eq,
    ‚Üê Ideal.Quotient.algebraMap_eq, ‚Üê IsScalarTower.algebraMap_apply]
#align ideal.inertia_deg_algebra_map Ideal.inertiaDeg_algebraMap

end DecEq

section FinrankQuotientMap

open scoped BigOperators

open scoped nonZeroDivisors

variable [Algebra R S]

variable {K : Type*} [Field K] [Algebra R K] [hRK : IsFractionRing R K]

variable {L : Type*} [Field L] [Algebra S L] [IsFractionRing S L]

variable {V V' V'' : Type*}

variable [AddCommGroup V] [Module R V] [Module K V] [IsScalarTower R K V]

variable [AddCommGroup V'] [Module R V'] [Module S V'] [IsScalarTower R S V']

variable [AddCommGroup V''] [Module R V'']

variable (K)

/-- Let `V` be a vector space over `K = Frac(R)`, `S / R` a ring extension
and `V'` a module over `S`. If `b`, in the intersection `V''` of `V` and `V'`,
is linear independent over `S` in `V'`, then it is linear independent over `R` in `V`.

The statement we prove is actually slightly more general:
 * it suffices that the inclusion `algebraMap R S : R ‚Üí S` is nontrivial
 * the function `f' : V'' ‚Üí V'` doesn't need to be injective
-/
theorem FinrankQuotientMap.linearIndependent_of_nontrivial [IsDomain R] [IsDedekindDomain R]
    (hRS : RingHom.ker (algebraMap R S) ‚â† ‚ä§) (f : V'' ‚Üí‚Çó[R] V) (hf : Function.Injective f)
    (f' : V'' ‚Üí‚Çó[R] V') {Œπ : Type*} {b : Œπ ‚Üí V''} (hb' : LinearIndependent S (f' ‚àò b)) :
    LinearIndependent K (f ‚àò b) := by
  contrapose! hb' with hb
  -- ‚ä¢ ¬¨LinearIndependent S (‚Üëf' ‚àò b)
  -- Informally, if we have a nontrivial linear dependence with coefficients `g` in `K`,
  -- then we can find a linear dependence with coefficients `I.Quotient.mk g'` in `R/I`,
  -- where `I = ker (algebraMap R S)`.
  -- We make use of the same principle but stay in `R` everywhere.
  simp only [linearIndependent_iff', not_forall] at hb ‚ä¢
  -- ‚ä¢ ‚àÉ x x_1 h x_2 x, ¬¨x_1 x_2 = 0
  obtain ‚ü®s, g, eq, j', hj's, hj'g‚ü© := hb
  -- ‚ä¢ ‚àÉ x x_1 h x_2 x, ¬¨x_1 x_2 = 0
  use s
  -- ‚ä¢ ‚àÉ x h x_1 x_2, ¬¨x x_1 = 0
  obtain ‚ü®a, hag, j, hjs, hgI‚ü© := Ideal.exist_integer_multiples_not_mem hRS s g hj's hj'g
  -- ‚ä¢ ‚àÉ x h x_1 x_2, ¬¨x x_1 = 0
  choose g'' hg'' using hag
  -- ‚ä¢ ‚àÉ x h x_1 x_2, ¬¨x x_1 = 0
  letI := Classical.propDecidable
  -- ‚ä¢ ‚àÉ x h x_1 x_2, ¬¨x x_1 = 0
  let g' i := if h : i ‚àà s then g'' i h else 0
  -- ‚ä¢ ‚àÉ x h x_1 x_2, ¬¨x x_1 = 0
  have hg' : ‚àÄ i ‚àà s, algebraMap _ _ (g' i) = a * g i := by
    intro i hi; exact (congr_arg _ (dif_pos hi)).trans (hg'' i hi)
  -- Because `R/I` is nontrivial, we can lift `g` to a nontrivial linear dependence in `S`.
  have hgI : algebraMap R S (g' j) ‚â† 0 := by
    simp only [FractionalIdeal.mem_coeIdeal, not_exists, not_and'] at hgI
    exact hgI _ (hg' j hjs)
  refine ‚ü®fun i => algebraMap R S (g' i), ?_, j, hjs, hgI‚ü©
  -- ‚ä¢ ‚àë i in s, (fun i => ‚Üë(algebraMap R S) (g' i)) i ‚Ä¢ (‚Üëf' ‚àò b) i = 0
  have eq : f (‚àë i in s, g' i ‚Ä¢ b i) = 0 := by
    rw [LinearMap.map_sum, ‚Üê smul_zero a, ‚Üê eq, Finset.smul_sum]
    refine Finset.sum_congr rfl ?_
    intro i hi
    rw [LinearMap.map_smul, ‚Üê IsScalarTower.algebraMap_smul K, hg' i hi, ‚Üê smul_assoc,
      smul_eq_mul, Function.comp_apply]
  simp only [IsScalarTower.algebraMap_smul, ‚Üê LinearMap.map_smul, ‚Üê LinearMap.map_sum,
    (f.map_eq_zero_iff hf).mp eq, LinearMap.map_zero, (¬∑ ‚àò ¬∑)]
#align ideal.finrank_quotient_map.linear_independent_of_nontrivial Ideal.FinrankQuotientMap.linearIndependent_of_nontrivial

open scoped Matrix

variable {K}

/-- If `b` mod `p` spans `S/p` as `R/p`-space, then `b` itself spans `Frac(S)` as `K`-space.

Here,
 * `p` is an ideal of `R` such that `R / p` is nontrivial
 * `K` is a field that has an embedding of `R` (in particular we can take `K = Frac(R)`)
 * `L` is a field extension of `K`
 * `S` is the integral closure of `R` in `L`

More precisely, we avoid quotients in this statement and instead require that `b ‚à™ pS` spans `S`.
-/
theorem FinrankQuotientMap.span_eq_top [IsDomain R] [IsDomain S] [Algebra K L] [IsNoetherian R S]
    [Algebra R L] [IsScalarTower R S L] [IsScalarTower R K L] [IsIntegralClosure S R L]
    [NoZeroSMulDivisors R K] (hp : p ‚â† ‚ä§) (b : Set S)
    (hb' : Submodule.span R b ‚äî (p.map (algebraMap R S)).restrictScalars R = ‚ä§) :
    Submodule.span K (algebraMap S L '' b) = ‚ä§ := by
  have hRL : Function.Injective (algebraMap R L) := by
    rw [IsScalarTower.algebraMap_eq R K L]
    exact (algebraMap K L).injective.comp (NoZeroSMulDivisors.algebraMap_injective R K)
  -- Let `M` be the `R`-module spanned by the proposed basis elements.
  let M : Submodule R S := Submodule.span R b
  -- ‚ä¢ Submodule.span K (‚Üë(algebraMap S L) '' b) = ‚ä§
  -- Then `S / M` is generated by some finite set of `n` vectors `a`.
  letI h : Module.Finite R (S ‚ß∏ M) :=
    Module.Finite.of_surjective (Submodule.mkQ _) (Submodule.Quotient.mk_surjective _)
  obtain ‚ü®n, a, ha‚ü© := @Module.Finite.exists_fin _ _ _ _ _ h
  -- ‚ä¢ Submodule.span K (‚Üë(algebraMap S L) '' b) = ‚ä§
  -- Because the image of `p` in `S / M` is `‚ä§`,
  have smul_top_eq : p ‚Ä¢ (‚ä§ : Submodule R (S ‚ß∏ M)) = ‚ä§ := by
    calc
      p ‚Ä¢ ‚ä§ = Submodule.map M.mkQ (p ‚Ä¢ ‚ä§) := by
        rw [Submodule.map_smul'', Submodule.map_top, M.range_mkQ]
      _ = ‚ä§ := by rw [Ideal.smul_top_eq_map, (Submodule.map_mkQ_eq_top M _).mpr hb']
  -- we can write the elements of `a` as `p`-linear combinations of other elements of `a`.
  have exists_sum : ‚àÄ x : S ‚ß∏ M, ‚àÉ a' : Fin n ‚Üí R, (‚àÄ i, a' i ‚àà p) ‚àß ‚àë i, a' i ‚Ä¢ a i = x := by
    intro x
    obtain ‚ü®a'', ha'', hx‚ü© := (Submodule.mem_ideal_smul_span_iff_exists_sum p a x).1
      (by { rw [ha, smul_top_eq]; exact Submodule.mem_top } :
        x ‚àà p ‚Ä¢ Submodule.span R (Set.range a))
    ¬∑ refine ‚ü®fun i => a'' i, fun i => ha'' _, ?_‚ü©
      rw [‚Üê hx, Finsupp.sum_fintype]
      exact fun _ => zero_smul _ _
  choose A' hA'p hA' using fun i => exists_sum (a i)
  -- ‚ä¢ Submodule.span K (‚Üë(algebraMap S L) '' b) = ‚ä§
  -- This gives us a(n invertible) matrix `A` such that `det A ‚àà (M = span R b)`,
  let A : Matrix (Fin n) (Fin n) R := Matrix.of A' - 1
  -- ‚ä¢ Submodule.span K (‚Üë(algebraMap S L) '' b) = ‚ä§
  let B := A.adjugate
  -- ‚ä¢ Submodule.span K (‚Üë(algebraMap S L) '' b) = ‚ä§
  have A_smul : ‚àÄ i, ‚àë j, A i j ‚Ä¢ a j = 0 := by
    intros
    simp only [Matrix.sub_apply, Matrix.of_apply, ne_eq, Matrix.one_apply, sub_smul,
      Finset.sum_sub_distrib, hA', Finset.sum_univ_ite, sub_self]
  -- since `span S {det A} / M = 0`.
  have d_smul : ‚àÄ i, A.det ‚Ä¢ a i = 0 := by
    intro i
    calc
      A.det ‚Ä¢ a i = ‚àë j, (B * A) i j ‚Ä¢ a j := ?_
      _ = ‚àë k, B i k ‚Ä¢ ‚àë j, A k j ‚Ä¢ a j := ?_
      _ = 0 := Finset.sum_eq_zero fun k _ => ?_
    ¬∑ simp only [Matrix.adjugate_mul, Matrix.smul_apply, Matrix.one_apply, smul_eq_mul, ite_true,
        mul_ite, mul_one, mul_zero, ite_smul, zero_smul, Finset.sum_ite_eq, Finset.mem_univ]
    ¬∑ simp only [Matrix.mul_apply, Finset.smul_sum, Finset.sum_smul, smul_smul]
      rw [Finset.sum_comm]
    ¬∑ rw [A_smul, smul_zero]
  -- In the rings of integers we have the desired inclusion.
  have span_d : (Submodule.span S ({algebraMap R S A.det} : Set S)).restrictScalars R ‚â§ M := by
    intro x hx
    rw [Submodule.restrictScalars_mem] at hx
    obtain ‚ü®x', rfl‚ü© := Submodule.mem_span_singleton.mp hx
    rw [smul_eq_mul, mul_comm, ‚Üê Algebra.smul_def] at hx ‚ä¢
    rw [‚Üê Submodule.Quotient.mk_eq_zero, Submodule.Quotient.mk_smul]
    obtain ‚ü®a', _, quot_x_eq‚ü© := exists_sum (Submodule.Quotient.mk x')
    rw [‚Üê quot_x_eq, Finset.smul_sum]
    conv =>
      lhs; congr; next => skip
      intro x; rw [smul_comm A.det, d_smul, smul_zero]
    exact Finset.sum_const_zero
  refine top_le_iff.mp
      (calc
        ‚ä§ = (Ideal.span {algebraMap R L A.det}).restrictScalars K := ?_
        _ ‚â§ Submodule.span K (algebraMap S L '' b) := ?_)
  -- Because `det A ‚â† 0`, we have `span L {det A} = ‚ä§`.
  ¬∑ rw [eq_comm, Submodule.restrictScalars_eq_top_iff, Ideal.span_singleton_eq_top]
    -- ‚ä¢ IsUnit (‚Üë(algebraMap R L) (Matrix.det A))
    refine IsUnit.mk0 _ ((map_ne_zero_iff (algebraMap R L) hRL).mpr ?_)
    -- ‚ä¢ Matrix.det A ‚â† 0
    refine @ne_zero_of_map _ _ _ _ _ _ (Ideal.Quotient.mk p) _ ?_
    -- ‚ä¢ ‚Üë(Quotient.mk p) (Matrix.det A) ‚â† 0
    haveI := Ideal.Quotient.nontrivial hp
    -- ‚ä¢ ‚Üë(Quotient.mk p) (Matrix.det A) ‚â† 0
    calc
      Ideal.Quotient.mk p A.det = Matrix.det ((Ideal.Quotient.mk p).mapMatrix A) := by
        rw [RingHom.map_det, RingHom.mapMatrix_apply]
      _ = Matrix.det ((Ideal.Quotient.mk p).mapMatrix (Matrix.of A' - 1)) := rfl
      _ = Matrix.det fun i j =>
          (Ideal.Quotient.mk p) (A' i j) - (1 : Matrix (Fin n) (Fin n) (R ‚ß∏ p)) i j := ?_
      _ = Matrix.det (-1 : Matrix (Fin n) (Fin n) (R ‚ß∏ p)) := ?_
      _ = (-1 : R ‚ß∏ p) ^ n := by rw [Matrix.det_neg, Fintype.card_fin, Matrix.det_one, mul_one]
      _ ‚â† 0 := IsUnit.ne_zero (isUnit_one.neg.pow _)
    ¬∑ refine congr_arg Matrix.det (Matrix.ext fun i j => ?_)
      -- ‚ä¢ ‚Üë(RingHom.mapMatrix (Quotient.mk p)) (‚ÜëMatrix.of A' - 1) i j = ‚Üë(Quotient.mk ‚Ä¶
      rw [map_sub, RingHom.mapMatrix_apply, map_one]
      -- ‚ä¢ (Matrix.map (‚ÜëMatrix.of A') ‚Üë(Quotient.mk p) - 1) i j = ‚Üë(Quotient.mk p) (A' ‚Ä¶
      rfl
      -- üéâ no goals
    ¬∑ refine congr_arg Matrix.det (Matrix.ext fun i j => ?_)
      -- ‚ä¢ ‚Üë(Quotient.mk p) (A' i j) - OfNat.ofNat 1 i j = (-1) i j
      rw [Ideal.Quotient.eq_zero_iff_mem.mpr (hA'p i j), zero_sub]
      -- ‚ä¢ -OfNat.ofNat 1 i j = (-1) i j
      rfl
      -- üéâ no goals
  -- And we conclude `L = span L {det A} ‚â§ span K b`, so `span K b` spans everything.
  ¬∑ intro x hx
    -- ‚ä¢ x ‚àà Submodule.span K (‚Üë(algebraMap S L) '' b)
    rw [Submodule.restrictScalars_mem, IsScalarTower.algebraMap_apply R S L] at hx
    -- ‚ä¢ x ‚àà Submodule.span K (‚Üë(algebraMap S L) '' b)
    refine' IsFractionRing.ideal_span_singleton_map_subset R _ hRL span_d hx
    -- ‚ä¢ Algebra.IsAlgebraic R L
    haveI : NoZeroSMulDivisors R L := NoZeroSMulDivisors.of_algebraMap_injective hRL
    -- ‚ä¢ Algebra.IsAlgebraic R L
    rw [‚Üê IsFractionRing.isAlgebraic_iff' R S]
    -- ‚ä¢ Algebra.IsAlgebraic R S
    intro x
    -- ‚ä¢ IsAlgebraic R x
    exact IsIntegral.isAlgebraic _ (isIntegral_of_noetherian inferInstance _)
    -- üéâ no goals
#align ideal.finrank_quotient_map.span_eq_top Ideal.FinrankQuotientMap.span_eq_top

variable (K L)

/-- If `p` is a maximal ideal of `R`, and `S` is the integral closure of `R` in `L`,
then the dimension `[S/pS : R/p]` is equal to `[Frac(S) : Frac(R)]`. -/
theorem finrank_quotient_map [IsDomain R] [IsDomain S] [IsDedekindDomain R] [Algebra K L]
    [Algebra R L] [IsScalarTower R K L] [IsScalarTower R S L] [IsIntegralClosure S R L]
    [hp : p.IsMaximal] [IsNoetherian R S] :
    finrank (R ‚ß∏ p) (S ‚ß∏ map (algebraMap R S) p) = finrank K L := by
  -- Choose an arbitrary basis `b` for `[S/pS : R/p]`.
  -- We'll use the previous results to turn it into a basis on `[Frac(S) : Frac(R)]`.
  letI : Field (R ‚ß∏ p) := Ideal.Quotient.field _
  -- ‚ä¢ finrank (R ‚ß∏ p) (S ‚ß∏ map (algebraMap R S) p) = finrank K L
  let Œπ := Module.Free.ChooseBasisIndex (R ‚ß∏ p) (S ‚ß∏ map (algebraMap R S) p)
  -- ‚ä¢ finrank (R ‚ß∏ p) (S ‚ß∏ map (algebraMap R S) p) = finrank K L
  let b : Basis Œπ (R ‚ß∏ p) (S ‚ß∏ map (algebraMap R S) p) := Module.Free.chooseBasis _ _
  -- ‚ä¢ finrank (R ‚ß∏ p) (S ‚ß∏ map (algebraMap R S) p) = finrank K L
  -- Namely, choose a representative `b' i : S` for each `b i : S / pS`.
  let b' : Œπ ‚Üí S := fun i => (Ideal.Quotient.mk_surjective (b i)).choose
  -- ‚ä¢ finrank (R ‚ß∏ p) (S ‚ß∏ map (algebraMap R S) p) = finrank K L
  have b_eq_b' : ‚áëb = (Submodule.mkQ (map (algebraMap R S) p)).restrictScalars R ‚àò b' :=
    funext fun i => (Ideal.Quotient.mk_surjective (b i)).choose_spec.symm
  -- We claim `b'` is a basis for `Frac(S)` over `Frac(R)` because it is linear independent
  -- and spans the whole of `Frac(S)`.
  let b'' : Œπ ‚Üí L := algebraMap S L ‚àò b'
  -- ‚ä¢ finrank (R ‚ß∏ p) (S ‚ß∏ map (algebraMap R S) p) = finrank K L
  have b''_li : LinearIndependent K b'' := ?_
  -- ‚ä¢ finrank (R ‚ß∏ p) (S ‚ß∏ map (algebraMap R S) p) = finrank K L
  have b''_sp : Submodule.span K (Set.range b'') = ‚ä§ := ?_
  -- Since the two bases have the same index set, the spaces have the same dimension.
  let c : Basis Œπ K L := Basis.mk b''_li b''_sp.ge
  rw [finrank_eq_card_basis b, finrank_eq_card_basis c]
  -- ‚ä¢ Submodule.span K (Set.range b'') = ‚ä§
  -- It remains to show that the basis is indeed linear independent and spans the whole space.
  ¬∑ rw [Set.range_comp]
    -- ‚ä¢ Submodule.span K (‚Üë(algebraMap S L) '' Set.range b') = ‚ä§
    refine FinrankQuotientMap.span_eq_top p hp.ne_top _ (top_le_iff.mp ?_)
    -- ‚ä¢ ‚ä§ ‚â§ Submodule.span R (Set.range b') ‚äî Submodule.restrictScalars R (map (alge ‚Ä¶
    -- The nicest way to show `S ‚â§ span b' ‚äî pS` is by reducing both sides modulo pS.
    -- However, this would imply distinguishing between `pS` as `S`-ideal,
    -- and `pS` as `R`-submodule, since they have different (non-defeq) quotients.
    -- Instead we'll lift `x mod pS ‚àà span b` to `y ‚àà span b'` for some `y - x ‚àà pS`.
    intro x _
    -- ‚ä¢ x ‚àà Submodule.span R (Set.range b') ‚äî Submodule.restrictScalars R (map (alge ‚Ä¶
    have mem_span_b : ((Submodule.mkQ (map (algebraMap R S) p)) x : S ‚ß∏ map (algebraMap R S) p) ‚àà
        Submodule.span (R ‚ß∏ p) (Set.range b) := b.mem_span _
    rw [‚Üê @Submodule.restrictScalars_mem R,
      Submodule.restrictScalars_span R (R ‚ß∏ p) Ideal.Quotient.mk_surjective, b_eq_b',
      Set.range_comp, ‚Üê Submodule.map_span] at mem_span_b
    obtain ‚ü®y, y_mem, y_eq‚ü© := Submodule.mem_map.mp mem_span_b
    -- ‚ä¢ x ‚àà Submodule.span R (Set.range b') ‚äî Submodule.restrictScalars R (map (alge ‚Ä¶
    suffices y + -(y - x) ‚àà _ by simpa
    -- ‚ä¢ y + -(y - x) ‚àà Submodule.span R (Set.range fun i => Exists.choose (_ : ‚àÉ a,  ‚Ä¶
    rw [LinearMap.restrictScalars_apply, Submodule.mkQ_apply, Submodule.mkQ_apply,
      Submodule.Quotient.eq] at y_eq
    exact add_mem (Submodule.mem_sup_left y_mem) (neg_mem <| Submodule.mem_sup_right y_eq)
    -- üéâ no goals
  ¬∑ have := b.linearIndependent; rw [b_eq_b'] at this
    -- ‚ä¢ LinearIndependent K b''
                                 -- ‚ä¢ LinearIndependent K b''
    convert FinrankQuotientMap.linearIndependent_of_nontrivial K _
        ((Algebra.linearMap S L).restrictScalars R) _ ((Submodule.mkQ _).restrictScalars R) this
    ¬∑ rw [Quotient.algebraMap_eq, Ideal.mk_ker]
      -- ‚ä¢ p ‚â† ‚ä§
      exact hp.ne_top
      -- üéâ no goals
    ¬∑ exact IsFractionRing.injective S L
      -- üéâ no goals
#align ideal.finrank_quotient_map Ideal.finrank_quotient_map

end FinrankQuotientMap

section FactLeComap

local notation "e" => ramificationIdx f p P

/-- `R / p` has a canonical map to `S / (P ^ e)`, where `e` is the ramification index
of `P` over `p`. -/
noncomputable instance Quotient.algebraQuotientPowRamificationIdx : Algebra (R ‚ß∏ p) (S ‚ß∏ P ^ e) :=
  Quotient.algebraQuotientOfLeComap (Ideal.map_le_iff_le_comap.mp le_pow_ramificationIdx)
#align ideal.quotient.algebra_quotient_pow_ramification_idx Ideal.Quotient.algebraQuotientPowRamificationIdx

@[simp]
theorem Quotient.algebraMap_quotient_pow_ramificationIdx (x : R) :
    algebraMap (R ‚ß∏ p) (S ‚ß∏ P ^ e) (Ideal.Quotient.mk p x) = Ideal.Quotient.mk _ (f x) := rfl
#align ideal.quotient.algebra_map_quotient_pow_ramification_idx Ideal.Quotient.algebraMap_quotient_pow_ramificationIdx

variable [hfp : NeZero (ramificationIdx f p P)]

/-- If `P` lies over `p`, then `R / p` has a canonical map to `S / P`.

This can't be an instance since the map `f : R ‚Üí S` is generally not inferrable.
-/
def Quotient.algebraQuotientOfRamificationIdxNeZero : Algebra (R ‚ß∏ p) (S ‚ß∏ P) :=
  Quotient.algebraQuotientOfLeComap (le_comap_of_ramificationIdx_ne_zero hfp.out)
#align ideal.quotient.algebra_quotient_of_ramification_idx_ne_zero Ideal.Quotient.algebraQuotientOfRamificationIdxNeZero

set_option synthInstance.checkSynthOrder false -- Porting note: this is okay by the remark below
-- In this file, the value for `f` can be inferred.
attribute [local instance] Ideal.Quotient.algebraQuotientOfRamificationIdxNeZero

@[simp]
theorem Quotient.algebraMap_quotient_of_ramificationIdx_neZero (x : R) :
    algebraMap (R ‚ß∏ p) (S ‚ß∏ P) (Ideal.Quotient.mk p x) = Ideal.Quotient.mk _ (f x) := rfl
#align ideal.quotient.algebra_map_quotient_of_ramification_idx_ne_zero Ideal.Quotient.algebraMap_quotient_of_ramificationIdx_neZero

/-- The inclusion `(P^(i + 1) / P^e) ‚äÇ (P^i / P^e)`. -/
@[simps]
def powQuotSuccInclusion (i : ‚Ñï) :
    Ideal.map (Ideal.Quotient.mk (P ^ e)) (P ^ (i + 1)) ‚Üí‚Çó[R ‚ß∏ p]
    Ideal.map (Ideal.Quotient.mk (P ^ e)) (P ^ i) where
  toFun x := ‚ü®x, Ideal.map_mono (Ideal.pow_le_pow i.le_succ) x.2‚ü©
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
#align ideal.pow_quot_succ_inclusion Ideal.powQuotSuccInclusion

theorem powQuotSuccInclusion_injective (i : ‚Ñï) :
    Function.Injective (powQuotSuccInclusion f p P i) := by
  rw [‚Üê LinearMap.ker_eq_bot, LinearMap.ker_eq_bot']
  -- ‚ä¢ ‚àÄ (m : { x // x ‚àà map (Quotient.mk (P ^ e)) (P ^ (i + 1)) }), ‚Üë(powQuotSuccI ‚Ä¶
  rintro ‚ü®x, hx‚ü© hx0
  -- ‚ä¢ { val := x, property := hx } = 0
  rw [Subtype.ext_iff] at hx0 ‚ä¢
  -- ‚ä¢ ‚Üë{ val := x, property := hx } = ‚Üë0
  rwa [powQuotSuccInclusion_apply_coe] at hx0
  -- üéâ no goals
#align ideal.pow_quot_succ_inclusion_injective Ideal.powQuotSuccInclusion_injective

/-- `S ‚ß∏ P` embeds into the quotient by `P^(i+1) ‚ß∏ P^e` as a subspace of `P^i ‚ß∏ P^e`.
See `quotientToQuotientRangePowQuotSucc` for this as a linear map,
and `quotientRangePowQuotSuccInclusionEquiv` for this as a linear equivalence.
-/
noncomputable def quotientToQuotientRangePowQuotSuccAux {i : ‚Ñï} {a : S} (a_mem : a ‚àà P ^ i) :
    S ‚ß∏ P ‚Üí
      (P ^ i).map (Ideal.Quotient.mk (P ^ e)) ‚ß∏ LinearMap.range (powQuotSuccInclusion f p P i) :=
  Quotient.map' (fun x : S => ‚ü®_, Ideal.mem_map_of_mem _ (Ideal.mul_mem_left _ x a_mem)‚ü©)
    fun x y h => by
    rw [Submodule.quotientRel_r_def] at h ‚ä¢
    -- ‚ä¢ (fun x => { val := ‚Üë(Quotient.mk (P ^ e)) (x * a), property := (_ : ‚Üë(Quotie ‚Ä¶
    simp only [_root_.map_mul, LinearMap.mem_range]
    -- ‚ä¢ ‚àÉ y_1, ‚Üë(powQuotSuccInclusion f p P i) y_1 = { val := ‚Üë(Quotient.mk (P ^ e)) ‚Ä¶
    refine' ‚ü®‚ü®_, Ideal.mem_map_of_mem _ (Ideal.mul_mem_mul h a_mem)‚ü©, _‚ü©
    -- ‚ä¢ ‚Üë(powQuotSuccInclusion f p P i) { val := ‚Üë(Quotient.mk (P ^ e)) ((x - y) * a ‚Ä¶
    ext
    -- ‚ä¢ ‚Üë(‚Üë(powQuotSuccInclusion f p P i) { val := ‚Üë(Quotient.mk (P ^ e)) ((x - y) * ‚Ä¶
    rw [powQuotSuccInclusion_apply_coe, Subtype.coe_mk, Submodule.coe_sub, Subtype.coe_mk,
      Subtype.coe_mk, _root_.map_mul, map_sub, sub_mul]
#align ideal.quotient_to_quotient_range_pow_quot_succ_aux Ideal.quotientToQuotientRangePowQuotSuccAux

theorem quotientToQuotientRangePowQuotSuccAux_mk {i : ‚Ñï} {a : S} (a_mem : a ‚àà P ^ i) (x : S) :
    quotientToQuotientRangePowQuotSuccAux f p P a_mem (Submodule.Quotient.mk x) =
      Submodule.Quotient.mk ‚ü®_, Ideal.mem_map_of_mem _ (Ideal.mul_mem_left _ x a_mem)‚ü© :=
  by apply Quotient.map'_mk''
     -- üéâ no goals
#align ideal.quotient_to_quotient_range_pow_quot_succ_aux_mk Ideal.quotientToQuotientRangePowQuotSuccAux_mk

/-- `S ‚ß∏ P` embeds into the quotient by `P^(i+1) ‚ß∏ P^e` as a subspace of `P^i ‚ß∏ P^e`. -/
noncomputable def quotientToQuotientRangePowQuotSucc {i : ‚Ñï} {a : S} (a_mem : a ‚àà P ^ i) :
    S ‚ß∏ P ‚Üí‚Çó[R ‚ß∏ p]
      (P ^ i).map (Ideal.Quotient.mk (P ^ e)) ‚ß∏ LinearMap.range (powQuotSuccInclusion f p P i) where
  toFun := quotientToQuotientRangePowQuotSuccAux f p P a_mem
  map_add' := by
    intro x y; refine' Quotient.inductionOn' x fun x => Quotient.inductionOn' y fun y => _
    -- ‚ä¢ quotientToQuotientRangePowQuotSuccAux f p P a_mem (x + y) = quotientToQuotie ‚Ä¶
               -- ‚ä¢ quotientToQuotientRangePowQuotSuccAux f p P a_mem (Quotient.mk'' x + Quotien ‚Ä¶
    simp only [Submodule.Quotient.mk''_eq_mk, ‚Üê Submodule.Quotient.mk_add,
      quotientToQuotientRangePowQuotSuccAux_mk, add_mul]
    exact congr_arg Submodule.Quotient.mk rfl
    -- üéâ no goals
  map_smul' := by
    intro x y; refine' Quotient.inductionOn' x fun x => Quotient.inductionOn' y fun y => _
    -- ‚ä¢ AddHom.toFun { toFun := quotientToQuotientRangePowQuotSuccAux f p P a_mem, m ‚Ä¶
               -- ‚ä¢ AddHom.toFun { toFun := quotientToQuotientRangePowQuotSuccAux f p P a_mem, m ‚Ä¶
    simp only [Submodule.Quotient.mk''_eq_mk, RingHom.id_apply,
      quotientToQuotientRangePowQuotSuccAux_mk]
    refine congr_arg Submodule.Quotient.mk ?_
    -- ‚ä¢ (fun x => { val := ‚Üë(Quotient.mk (P ^ e)) (x * a), property := (_ : ‚Üë(Quotie ‚Ä¶
    ext
    -- ‚ä¢ ‚Üë((fun x => { val := ‚Üë(Quotient.mk (P ^ e)) (x * a), property := (_ : ‚Üë(Quot ‚Ä¶
    simp only [mul_assoc, _root_.map_mul, Quotient.mk_eq_mk, Submodule.coe_smul_of_tower,
      Algebra.smul_def, Quotient.algebraMap_quotient_pow_ramificationIdx]
#align ideal.quotient_to_quotient_range_pow_quot_succ Ideal.quotientToQuotientRangePowQuotSucc

theorem quotientToQuotientRangePowQuotSucc_mk {i : ‚Ñï} {a : S} (a_mem : a ‚àà P ^ i) (x : S) :
    quotientToQuotientRangePowQuotSucc f p P a_mem (Submodule.Quotient.mk x) =
      Submodule.Quotient.mk ‚ü®_, Ideal.mem_map_of_mem _ (Ideal.mul_mem_left _ x a_mem)‚ü© :=
  quotientToQuotientRangePowQuotSuccAux_mk f p P a_mem x
#align ideal.quotient_to_quotient_range_pow_quot_succ_mk Ideal.quotientToQuotientRangePowQuotSucc_mk

theorem quotientToQuotientRangePowQuotSucc_injective [IsDomain S] [IsDedekindDomain S] [P.IsPrime]
    {i : ‚Ñï} (hi : i < e) {a : S} (a_mem : a ‚àà P ^ i) (a_not_mem : a ‚àâ P ^ (i + 1)) :
    Function.Injective (quotientToQuotientRangePowQuotSucc f p P a_mem) := fun x =>
  Quotient.inductionOn' x fun x y =>
    Quotient.inductionOn' y fun y h => by
      have Pe_le_Pi1 : P ^ e ‚â§ P ^ (i + 1) := Ideal.pow_le_pow hi
      -- ‚ä¢ Quotient.mk'' x = Quotient.mk'' y
      simp only [Submodule.Quotient.mk''_eq_mk, quotientToQuotientRangePowQuotSucc_mk,
        Submodule.Quotient.eq, LinearMap.mem_range, Subtype.ext_iff, Subtype.coe_mk,
        Submodule.coe_sub] at h ‚ä¢
      rcases h with ‚ü®‚ü®‚ü®z‚ü©, hz‚ü©, h‚ü©
      -- ‚ä¢ x - y ‚àà P
      rw [Submodule.Quotient.quot_mk_eq_mk, Ideal.Quotient.mk_eq_mk, Ideal.mem_quotient_iff_mem_sup,
        sup_eq_left.mpr Pe_le_Pi1] at hz
      rw [powQuotSuccInclusion_apply_coe, Subtype.coe_mk, Submodule.Quotient.quot_mk_eq_mk,
        Ideal.Quotient.mk_eq_mk, ‚Üê map_sub, Ideal.Quotient.eq, ‚Üê sub_mul] at h
      exact
        (Ideal.IsPrime.mul_mem_pow _
              ((Submodule.sub_mem_iff_right _ hz).mp (Pe_le_Pi1 h))).resolve_right
          a_not_mem
#align ideal.quotient_to_quotient_range_pow_quot_succ_injective Ideal.quotientToQuotientRangePowQuotSucc_injective

theorem quotientToQuotientRangePowQuotSucc_surjective [IsDomain S] [IsDedekindDomain S]
    (hP0 : P ‚â† ‚ä•) [hP : P.IsPrime] {i : ‚Ñï} (hi : i < e) {a : S} (a_mem : a ‚àà P ^ i)
    (a_not_mem : a ‚àâ P ^ (i + 1)) :
    Function.Surjective (quotientToQuotientRangePowQuotSucc f p P a_mem) := by
  rintro ‚ü®‚ü®‚ü®x‚ü©, hx‚ü©‚ü©
  -- ‚ä¢ ‚àÉ a_1, ‚Üë(quotientToQuotientRangePowQuotSucc f p P a_mem) a_1 = Quot.mk Setoi ‚Ä¶
  have Pe_le_Pi : P ^ e ‚â§ P ^ i := Ideal.pow_le_pow hi.le
  -- ‚ä¢ ‚àÉ a_1, ‚Üë(quotientToQuotientRangePowQuotSucc f p P a_mem) a_1 = Quot.mk Setoi ‚Ä¶
  rw [Submodule.Quotient.quot_mk_eq_mk, Ideal.Quotient.mk_eq_mk, Ideal.mem_quotient_iff_mem_sup,
    sup_eq_left.mpr Pe_le_Pi] at hx
  suffices hx' : x ‚àà Ideal.span {a} ‚äî P ^ (i + 1)
  -- ‚ä¢ ‚àÉ a_1, ‚Üë(quotientToQuotientRangePowQuotSucc f p P a_mem) a_1 = Quot.mk Setoi ‚Ä¶
  ¬∑ obtain ‚ü®y', hy', z, hz, rfl‚ü© := Submodule.mem_sup.mp hx'
    -- ‚ä¢ ‚àÉ a_1, ‚Üë(quotientToQuotientRangePowQuotSucc f p P a_mem) a_1 = Quot.mk Setoi ‚Ä¶
    obtain ‚ü®y, rfl‚ü© := Ideal.mem_span_singleton.mp hy'
    -- ‚ä¢ ‚àÉ a_1, ‚Üë(quotientToQuotientRangePowQuotSucc f p P a_mem) a_1 = Quot.mk Setoi ‚Ä¶
    refine ‚ü®Submodule.Quotient.mk y, ?_‚ü©
    -- ‚ä¢ ‚Üë(quotientToQuotientRangePowQuotSucc f p P a_mem) (Submodule.Quotient.mk y)  ‚Ä¶
    simp only [Submodule.Quotient.quot_mk_eq_mk, quotientToQuotientRangePowQuotSucc_mk,
      Submodule.Quotient.eq, LinearMap.mem_range, Subtype.ext_iff, Subtype.coe_mk,
      Submodule.coe_sub]
    refine ‚ü®‚ü®_, Ideal.mem_map_of_mem _ (Submodule.neg_mem _ hz)‚ü©, ?_‚ü©
    -- ‚ä¢ ‚Üë(‚Üë(powQuotSuccInclusion f p P i) { val := ‚Üë(Quotient.mk (P ^ e)) (-z), prop ‚Ä¶
    rw [powQuotSuccInclusion_apply_coe, Subtype.coe_mk, Ideal.Quotient.mk_eq_mk, map_add,
      mul_comm y a, sub_add_cancel', map_neg]
  letI := Classical.decEq (Ideal S)
  -- ‚ä¢ x ‚àà span {a} ‚äî P ^ (i + 1)
  rw [sup_eq_prod_inf_factors _ (pow_ne_zero _ hP0), normalizedFactors_pow,
    normalizedFactors_irreducible ((Ideal.prime_iff_isPrime hP0).mpr hP).irreducible, normalize_eq,
    Multiset.nsmul_singleton, Multiset.inter_replicate, Multiset.prod_replicate]
  rw [‚Üê Submodule.span_singleton_le_iff_mem, Ideal.submodule_span_eq] at a_mem a_not_mem
  -- ‚ä¢ x ‚àà P ^ min (Multiset.count P (normalizedFactors (span {a}))) (i + 1)
  rwa [Ideal.count_normalizedFactors_eq a_mem a_not_mem, min_eq_left i.le_succ]
  -- ‚ä¢ span {a} ‚â† ‚ä•
  ¬∑ intro ha
    -- ‚ä¢ False
    rw [Ideal.span_singleton_eq_bot.mp ha] at a_not_mem
    -- ‚ä¢ False
    have := (P ^ (i + 1)).zero_mem
    -- ‚ä¢ False
    contradiction
    -- üéâ no goals
#align ideal.quotient_to_quotient_range_pow_quot_succ_surjective Ideal.quotientToQuotientRangePowQuotSucc_surjective

/-- Quotienting `P^i / P^e` by its subspace `P^(i+1) ‚ß∏ P^e` is
`R ‚ß∏ p`-linearly isomorphic to `S ‚ß∏ P`. -/
noncomputable def quotientRangePowQuotSuccInclusionEquiv [IsDomain S] [IsDedekindDomain S]
    [P.IsPrime] (hP : P ‚â† ‚ä•) {i : ‚Ñï} (hi : i < e) :
    ((P ^ i).map (Ideal.Quotient.mk (P ^ e)) ‚ß∏ LinearMap.range (powQuotSuccInclusion f p P i))
      ‚âÉ‚Çó[R ‚ß∏ p] S ‚ß∏ P := by
  choose a a_mem a_not_mem using
    SetLike.exists_of_lt
      (Ideal.strictAnti_pow P hP (Ideal.IsPrime.ne_top inferInstance) (le_refl i.succ))
  refine' (LinearEquiv.ofBijective _ ‚ü®_, _‚ü©).symm
  ¬∑ exact quotientToQuotientRangePowQuotSucc f p P a_mem
    -- üéâ no goals
  ¬∑ exact quotientToQuotientRangePowQuotSucc_injective f p P hi a_mem a_not_mem
    -- üéâ no goals
  ¬∑ exact quotientToQuotientRangePowQuotSucc_surjective f p P hP hi a_mem a_not_mem
    -- üéâ no goals
#align ideal.quotient_range_pow_quot_succ_inclusion_equiv Ideal.quotientRangePowQuotSuccInclusionEquiv

/-- Since the inclusion `(P^(i + 1) / P^e) ‚äÇ (P^i / P^e)` has a kernel isomorphic to `P / S`,
`[P^i / P^e : R / p] = [P^(i+1) / P^e : R / p] + [P / S : R / p]` -/
theorem rank_pow_quot_aux [IsDomain S] [IsDedekindDomain S] [p.IsMaximal] [P.IsPrime] (hP0 : P ‚â† ‚ä•)
    {i : ‚Ñï} (hi : i < e) :
    Module.rank (R ‚ß∏ p) (Ideal.map (Ideal.Quotient.mk (P ^ e)) (P ^ i)) =
      Module.rank (R ‚ß∏ p) (S ‚ß∏ P) +
        Module.rank (R ‚ß∏ p) (Ideal.map (Ideal.Quotient.mk (P ^ e)) (P ^ (i + 1))) := by
  letI : Field (R ‚ß∏ p) := Ideal.Quotient.field _
  -- ‚ä¢ Module.rank (R ‚ß∏ p) { x // x ‚àà map (Quotient.mk (P ^ e)) (P ^ i) } = Module. ‚Ä¶
  rw [‚Üê rank_range_of_injective _ (powQuotSuccInclusion_injective f p P i),
    (quotientRangePowQuotSuccInclusionEquiv f p P hP0 hi).symm.rank_eq]
  exact (rank_quotient_add_rank (LinearMap.range (powQuotSuccInclusion f p P i))).symm
  -- üéâ no goals
#align ideal.rank_pow_quot_aux Ideal.rank_pow_quot_aux

theorem rank_pow_quot [IsDomain S] [IsDedekindDomain S] [p.IsMaximal] [P.IsPrime] (hP0 : P ‚â† ‚ä•)
    (i : ‚Ñï) (hi : i ‚â§ e) :
    Module.rank (R ‚ß∏ p) (Ideal.map (Ideal.Quotient.mk (P ^ e)) (P ^ i)) =
      (e - i) ‚Ä¢ Module.rank (R ‚ß∏ p) (S ‚ß∏ P) := by
-- Porting note: Lean cannot figure out what to prove by itself
  let Q : ‚Ñï ‚Üí Prop :=
    fun i => Module.rank (R ‚ß∏ p) { x // x ‚àà map (Quotient.mk (P ^ e)) (P ^ i) }
      = (e - i) ‚Ä¢ Module.rank (R ‚ß∏ p) (S ‚ß∏ P)
  refine @Nat.decreasingInduction' Q i e (fun j lt_e _le_j ih => ?_) hi ?_
  -- ‚ä¢ Q j
  ¬∑ dsimp only
    -- ‚ä¢ Module.rank (R ‚ß∏ p) { x // x ‚àà map (Quotient.mk (P ^ e)) (P ^ j) } = (e - j) ‚Ä¶
    rw [rank_pow_quot_aux f p P _ lt_e, ih, ‚Üê succ_nsmul, Nat.sub_succ, ‚Üê Nat.succ_eq_add_one,
      Nat.succ_pred_eq_of_pos (Nat.sub_pos_of_lt lt_e)]
    assumption
    -- üéâ no goals
  ¬∑ dsimp only
    -- ‚ä¢ Module.rank (R ‚ß∏ p) { x // x ‚àà map (Quotient.mk (P ^ e)) (P ^ e) } = (e - e) ‚Ä¶
    rw [Nat.sub_self, zero_nsmul, map_quotient_self]
    -- ‚ä¢ Module.rank (R ‚ß∏ p) { x // x ‚àà ‚ä• } = 0
    exact rank_bot (R ‚ß∏ p) (S ‚ß∏ P ^ e)
    -- üéâ no goals
#align ideal.rank_pow_quot Ideal.rank_pow_quot

/-- If `p` is a maximal ideal of `R`, `S` extends `R` and `P^e` lies over `p`,
then the dimension `[S/(P^e) : R/p]` is equal to `e * [S/P : R/p]`. -/
theorem rank_prime_pow_ramificationIdx [IsDomain S] [IsDedekindDomain S] [p.IsMaximal] [P.IsPrime]
    (hP0 : P ‚â† ‚ä•) (he : e ‚â† 0) :
    Module.rank (R ‚ß∏ p) (S ‚ß∏ P ^ e) =
      e ‚Ä¢
        @Module.rank (R ‚ß∏ p) (S ‚ß∏ P) _ _
          (@Algebra.toModule _ _ _ _ <|
            @Quotient.algebraQuotientOfRamificationIdxNeZero _ _ _ _ _ _ _ ‚ü®he‚ü©) := by
  letI : NeZero e := ‚ü®he‚ü©
  -- ‚ä¢ Module.rank (R ‚ß∏ p) (S ‚ß∏ P ^ e) = e ‚Ä¢ Module.rank (R ‚ß∏ p) (S ‚ß∏ P)
  have := rank_pow_quot f p P hP0 0 (Nat.zero_le e)
  -- ‚ä¢ Module.rank (R ‚ß∏ p) (S ‚ß∏ P ^ e) = e ‚Ä¢ Module.rank (R ‚ß∏ p) (S ‚ß∏ P)
  rw [pow_zero, Nat.sub_zero, Ideal.one_eq_top, Ideal.map_top] at this
  -- ‚ä¢ Module.rank (R ‚ß∏ p) (S ‚ß∏ P ^ e) = e ‚Ä¢ Module.rank (R ‚ß∏ p) (S ‚ß∏ P)
  exact (rank_top (R ‚ß∏ p) _).symm.trans this
  -- üéâ no goals
#align ideal.rank_prime_pow_ramification_idx Ideal.rank_prime_pow_ramificationIdx

/-- If `p` is a maximal ideal of `R`, `S` extends `R` and `P^e` lies over `p`,
then the dimension `[S/(P^e) : R/p]`, as a natural number, is equal to `e * [S/P : R/p]`. -/
theorem finrank_prime_pow_ramificationIdx [IsDomain S] [IsDedekindDomain S] (hP0 : P ‚â† ‚ä•)
    [p.IsMaximal] [P.IsPrime] (he : e ‚â† 0) :
    finrank (R ‚ß∏ p) (S ‚ß∏ P ^ e) =
      e *
        @finrank (R ‚ß∏ p) (S ‚ß∏ P) _ _
          (@Algebra.toModule _ _ _ _ <|
            @Quotient.algebraQuotientOfRamificationIdxNeZero _ _ _ _ _ _ _ ‚ü®he‚ü©) := by
  letI : NeZero e := ‚ü®he‚ü©
  -- ‚ä¢ finrank (R ‚ß∏ p) (S ‚ß∏ P ^ e) = e * finrank (R ‚ß∏ p) (S ‚ß∏ P)
  letI : Algebra (R ‚ß∏ p) (S ‚ß∏ P) := Quotient.algebraQuotientOfRamificationIdxNeZero f p P
  -- ‚ä¢ finrank (R ‚ß∏ p) (S ‚ß∏ P ^ e) = e * finrank (R ‚ß∏ p) (S ‚ß∏ P)
  letI := Ideal.Quotient.field p
  -- ‚ä¢ finrank (R ‚ß∏ p) (S ‚ß∏ P ^ e) = e * finrank (R ‚ß∏ p) (S ‚ß∏ P)
  have hdim := rank_prime_pow_ramificationIdx _ _ _ hP0 he
  -- ‚ä¢ finrank (R ‚ß∏ p) (S ‚ß∏ P ^ e) = e * finrank (R ‚ß∏ p) (S ‚ß∏ P)
  by_cases hP : FiniteDimensional (R ‚ß∏ p) (S ‚ß∏ P)
  -- ‚ä¢ finrank (R ‚ß∏ p) (S ‚ß∏ P ^ e) = e * finrank (R ‚ß∏ p) (S ‚ß∏ P)
  ¬∑ haveI := hP
    -- ‚ä¢ finrank (R ‚ß∏ p) (S ‚ß∏ P ^ e) = e * finrank (R ‚ß∏ p) (S ‚ß∏ P)
    haveI := (finiteDimensional_iff_of_rank_eq_nsmul he hdim).mpr hP
    -- ‚ä¢ finrank (R ‚ß∏ p) (S ‚ß∏ P ^ e) = e * finrank (R ‚ß∏ p) (S ‚ß∏ P)
    refine' Cardinal.natCast_injective _
    -- ‚ä¢ ‚Üë(finrank (R ‚ß∏ p) (S ‚ß∏ P ^ e)) = ‚Üë(e * finrank (R ‚ß∏ p) (S ‚ß∏ P))
    rw [finrank_eq_rank', Nat.cast_mul, finrank_eq_rank', hdim, nsmul_eq_mul]
    -- üéâ no goals
  have hPe := mt (finiteDimensional_iff_of_rank_eq_nsmul he hdim).mp hP
  -- ‚ä¢ finrank (R ‚ß∏ p) (S ‚ß∏ P ^ e) = e * finrank (R ‚ß∏ p) (S ‚ß∏ P)
  simp only [finrank_of_infinite_dimensional hP, finrank_of_infinite_dimensional hPe,
    mul_zero]
#align ideal.finrank_prime_pow_ramification_idx Ideal.finrank_prime_pow_ramificationIdx

end FactLeComap

section FactorsMap

open scoped Classical

/-! ## Properties of the factors of `p.map (algebraMap R S)` -/


variable [IsDomain S] [IsDedekindDomain S] [Algebra R S]

theorem Factors.ne_bot (P : (factors (map (algebraMap R S) p)).toFinset) : (P : Ideal S) ‚â† ‚ä• :=
  (prime_of_factor _ (Multiset.mem_toFinset.mp P.2)).ne_zero
#align ideal.factors.ne_bot Ideal.Factors.ne_bot

instance Factors.isPrime (P : (factors (map (algebraMap R S) p)).toFinset) :
    IsPrime (P : Ideal S) :=
  Ideal.isPrime_of_prime (prime_of_factor _ (Multiset.mem_toFinset.mp P.2))
#align ideal.factors.is_prime Ideal.Factors.isPrime

theorem Factors.ramificationIdx_ne_zero (P : (factors (map (algebraMap R S) p)).toFinset) :
    ramificationIdx (algebraMap R S) p P ‚â† 0 :=
  IsDedekindDomain.ramificationIdx_ne_zero (ne_zero_of_mem_factors (Multiset.mem_toFinset.mp P.2))
    (Factors.isPrime p P) (Ideal.le_of_dvd (dvd_of_mem_factors (Multiset.mem_toFinset.mp P.2)))
#align ideal.factors.ramification_idx_ne_zero Ideal.Factors.ramificationIdx_ne_zero

instance Factors.fact_ramificationIdx_neZero (P : (factors (map (algebraMap R S) p)).toFinset) :
    NeZero (ramificationIdx (algebraMap R S) p P) :=
  ‚ü®Factors.ramificationIdx_ne_zero p P‚ü©
#align ideal.factors.fact_ramification_idx_ne_zero Ideal.Factors.fact_ramificationIdx_neZero

set_option synthInstance.checkSynthOrder false
-- Porting note: this is okay since, as noted above, in this file the value of `f` can be inferred
attribute [local instance] Quotient.algebraQuotientOfRamificationIdxNeZero

instance Factors.isScalarTower (P : (factors (map (algebraMap R S) p)).toFinset) :
    IsScalarTower R (R ‚ß∏ p) (S ‚ß∏ (P : Ideal S)) :=
  IsScalarTower.of_algebraMap_eq fun x => by simp
                                             -- üéâ no goals
#align ideal.factors.is_scalar_tower Ideal.Factors.isScalarTower

attribute [local instance] Ideal.Quotient.field

theorem Factors.finrank_pow_ramificationIdx [p.IsMaximal]
    (P : (factors (map (algebraMap R S) p)).toFinset) :
    finrank (R ‚ß∏ p) (S ‚ß∏ (P : Ideal S) ^ ramificationIdx (algebraMap R S) p P) =
      ramificationIdx (algebraMap R S) p P * inertiaDeg (algebraMap R S) p P := by
  rw [finrank_prime_pow_ramificationIdx, inertiaDeg_algebraMap]
  exacts [NeZero.ne _, Factors.ne_bot p P]
  -- üéâ no goals
#align ideal.factors.finrank_pow_ramification_idx Ideal.Factors.finrank_pow_ramificationIdx

instance Factors.finiteDimensional_quotient [IsNoetherian R S] [p.IsMaximal]
    (P : (factors (map (algebraMap R S) p)).toFinset) :
    FiniteDimensional (R ‚ß∏ p) (S ‚ß∏ (P : Ideal S)) :=
  IsNoetherian.iff_fg.mp <|
    isNoetherian_of_tower R <|
      isNoetherian_of_surjective S (Ideal.Quotient.mk‚Çê _ _).toLinearMap <|
        LinearMap.range_eq_top.mpr Ideal.Quotient.mk_surjective
#align ideal.factors.finite_dimensional_quotient Ideal.Factors.finiteDimensional_quotient

theorem Factors.inertiaDeg_ne_zero [IsNoetherian R S] [p.IsMaximal]
    (P : (factors (map (algebraMap R S) p)).toFinset) : inertiaDeg (algebraMap R S) p P ‚â† 0 := by
  rw [inertiaDeg_algebraMap]; exact (FiniteDimensional.finrank_pos_iff.mpr inferInstance).ne'
  -- ‚ä¢ finrank (R ‚ß∏ p) (S ‚ß∏ ‚ÜëP) ‚â† 0
                              -- üéâ no goals
#align ideal.factors.inertia_deg_ne_zero Ideal.Factors.inertiaDeg_ne_zero

instance Factors.finiteDimensional_quotient_pow [IsNoetherian R S] [p.IsMaximal]
    (P : (factors (map (algebraMap R S) p)).toFinset) :
    FiniteDimensional (R ‚ß∏ p) (S ‚ß∏ (P : Ideal S) ^ ramificationIdx (algebraMap R S) p P) := by
  refine FiniteDimensional.finiteDimensional_of_finrank ?_
  -- ‚ä¢ 0 < finrank (R ‚ß∏ p) (S ‚ß∏ ‚ÜëP ^ ramificationIdx (algebraMap R S) p ‚ÜëP)
  rw [pos_iff_ne_zero, Factors.finrank_pow_ramificationIdx]
  -- ‚ä¢ ramificationIdx (algebraMap R S) p ‚ÜëP * inertiaDeg (algebraMap R S) p ‚ÜëP ‚â† 0
  exact mul_ne_zero (Factors.ramificationIdx_ne_zero p P) (Factors.inertiaDeg_ne_zero p P)
  -- üéâ no goals
#align ideal.factors.finite_dimensional_quotient_pow Ideal.Factors.finiteDimensional_quotient_pow

universe w

/-- **Chinese remainder theorem** for a ring of integers: if the prime ideal `p : Ideal R`
factors in `S` as `‚àè i, P i ^ e i`, then `S ‚ß∏ I` factors as `Œ† i, R ‚ß∏ (P i ^ e i)`. -/
noncomputable def Factors.piQuotientEquiv (p : Ideal R) (hp : map (algebraMap R S) p ‚â† ‚ä•) :
    S ‚ß∏ map (algebraMap R S) p ‚âÉ+*
      ‚àÄ P : (factors (map (algebraMap R S) p)).toFinset,
        S ‚ß∏ (P : Ideal S) ^ ramificationIdx (algebraMap R S) p P :=
  (IsDedekindDomain.quotientEquivPiFactors hp).trans <|
    @RingEquiv.piCongrRight (factors (map (algebraMap R S) p)).toFinset
      (fun P => S ‚ß∏ (P : Ideal S) ^ (factors (map (algebraMap R S) p)).count (P : Ideal S))
      (fun P => S ‚ß∏ (P : Ideal S) ^ ramificationIdx (algebraMap R S) p P) _ _
      fun P : (factors (map (algebraMap R S) p)).toFinset =>
      Ideal.quotEquivOfEq <| by
        rw [IsDedekindDomain.ramificationIdx_eq_factors_count hp (Factors.isPrime p P)
            (Factors.ne_bot p P)]
#align ideal.factors.pi_quotient_equiv Ideal.Factors.piQuotientEquiv

@[simp]
theorem Factors.piQuotientEquiv_mk (p : Ideal R) (hp : map (algebraMap R S) p ‚â† ‚ä•) (x : S) :
    Factors.piQuotientEquiv p hp (Ideal.Quotient.mk _ x) = fun _ => Ideal.Quotient.mk _ x := rfl
#align ideal.factors.pi_quotient_equiv_mk Ideal.Factors.piQuotientEquiv_mk

@[simp]
theorem Factors.piQuotientEquiv_map (p : Ideal R) (hp : map (algebraMap R S) p ‚â† ‚ä•) (x : R) :
    Factors.piQuotientEquiv p hp (algebraMap _ _ x) = fun _ =>
      Ideal.Quotient.mk _ (algebraMap _ _ x) := rfl
#align ideal.factors.pi_quotient_equiv_map Ideal.Factors.piQuotientEquiv_map

variable (S)

/-- **Chinese remainder theorem** for a ring of integers: if the prime ideal `p : Ideal R`
factors in `S` as `‚àè i, P i ^ e i`,
then `S ‚ß∏ I` factors `R ‚ß∏ I`-linearly as `Œ† i, R ‚ß∏ (P i ^ e i)`. -/
noncomputable def Factors.piQuotientLinearEquiv (p : Ideal R) (hp : map (algebraMap R S) p ‚â† ‚ä•) :
    (S ‚ß∏ map (algebraMap R S) p) ‚âÉ‚Çó[R ‚ß∏ p]
      ‚àÄ P : (factors (map (algebraMap R S) p)).toFinset,
        S ‚ß∏ (P : Ideal S) ^ ramificationIdx (algebraMap R S) p P :=
  { Factors.piQuotientEquiv p hp with
    map_smul' := by
      rintro ‚ü®c‚ü© ‚ü®x‚ü©; ext P
      -- ‚ä¢ AddHom.toFun { toFun := src‚úù.toFun, map_add' := (_ : ‚àÄ (x y : S ‚ß∏ map (algeb ‚Ä¶
                      -- ‚ä¢ AddHom.toFun { toFun := src‚úù.toFun, map_add' := (_ : ‚àÄ (x y : S ‚ß∏ map (algeb ‚Ä¶
      simp only [Submodule.Quotient.quot_mk_eq_mk, Quotient.mk_eq_mk, Algebra.smul_def,
        Quotient.algebraMap_quotient_map_quotient, Quotient.mk_algebraMap,
        RingHomCompTriple.comp_apply, Pi.mul_apply, Pi.algebraMap_apply]
      congr }
      -- üéâ no goals
#align ideal.factors.pi_quotient_linear_equiv Ideal.Factors.piQuotientLinearEquiv

variable {S}

open scoped BigOperators

/-- The **fundamental identity** of ramification index `e` and inertia degree `f`:
for `P` ranging over the primes lying over `p`, `‚àë P, e P * f P = [Frac(S) : Frac(R)]`;
here `S` is a finite `R`-module (and thus `Frac(S) : Frac(R)` is a finite extension) and `p`
is maximal.
-/
theorem sum_ramification_inertia (K L : Type*) [Field K] [Field L] [IsDomain R]
    [IsDedekindDomain R] [Algebra R K] [IsFractionRing R K] [Algebra S L] [IsFractionRing S L]
    [Algebra K L] [Algebra R L] [IsScalarTower R S L] [IsScalarTower R K L] [IsNoetherian R S]
    [IsIntegralClosure S R L] [p.IsMaximal] (hp0 : p ‚â† ‚ä•) :
    (‚àë P in (factors (map (algebraMap R S) p)).toFinset,
        ramificationIdx (algebraMap R S) p P * inertiaDeg (algebraMap R S) p P) =
      finrank K L := by
  set e := ramificationIdx (algebraMap R S) p
  -- ‚ä¢ ‚àë P in Multiset.toFinset (factors (map (algebraMap R S) p)), e P * inertiaDe ‚Ä¶
  set f := inertiaDeg (algebraMap R S) p
  -- ‚ä¢ ‚àë P in Multiset.toFinset (factors (map (algebraMap R S) p)), e P * f P = fin ‚Ä¶
  have inj_RL : Function.Injective (algebraMap R L) := by
    rw [IsScalarTower.algebraMap_eq R K L, RingHom.coe_comp]
    exact (RingHom.injective _).comp (IsFractionRing.injective R K)
  have inj_RS : Function.Injective (algebraMap R S) := by
    refine Function.Injective.of_comp (show Function.Injective (algebraMap S L ‚àò _) from ?_)
    rw [‚Üê RingHom.coe_comp, ‚Üê IsScalarTower.algebraMap_eq]
    exact inj_RL
  calc
    (‚àë P in (factors (map (algebraMap R S) p)).toFinset, e P * f P) =
        ‚àë P in (factors (map (algebraMap R S) p)).toFinset.attach,
          finrank (R ‚ß∏ p) (S ‚ß∏ (P : Ideal S) ^ e P) := ?_
    _ = finrank (R ‚ß∏ p)
          (‚àÄ P : (factors (map (algebraMap R S) p)).toFinset, S ‚ß∏ (P : Ideal S) ^ e P) :=
      (finrank_pi_fintype (R ‚ß∏ p)).symm
    _ = finrank (R ‚ß∏ p) (S ‚ß∏ map (algebraMap R S) p) := ?_
    _ = finrank K L := ?_
  ¬∑ rw [‚Üê Finset.sum_attach]
    -- ‚ä¢ ‚àë x in Finset.attach (Multiset.toFinset (factors (map (algebraMap R S) p))), ‚Ä¶
    refine Finset.sum_congr rfl fun P _ => ?_
    -- ‚ä¢ e ‚ÜëP * f ‚ÜëP = finrank (R ‚ß∏ p) (S ‚ß∏ ‚ÜëP ^ e ‚ÜëP)
    rw [Factors.finrank_pow_ramificationIdx]
    -- üéâ no goals
  ¬∑ refine LinearEquiv.finrank_eq (Factors.piQuotientLinearEquiv S p ?_).symm
    -- ‚ä¢ map (algebraMap R S) p ‚â† ‚ä•
    rwa [Ne.def, Ideal.map_eq_bot_iff_le_ker, (RingHom.injective_iff_ker_eq_bot _).mp inj_RS,
      le_bot_iff]
  ¬∑ exact finrank_quotient_map p K L
    -- üéâ no goals
#align ideal.sum_ramification_inertia Ideal.sum_ramification_inertia

end FactorsMap

end Ideal
