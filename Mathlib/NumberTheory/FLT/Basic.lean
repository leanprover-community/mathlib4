/-
Copyright (c) 2023 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Ya√´l Dillies, Jineon Baek
-/
import Mathlib.Algebra.EuclideanDomain.Int
import Mathlib.Algebra.GCDMonoid.Finset
import Mathlib.Algebra.GCDMonoid.Nat
import Mathlib.Algebra.Order.Ring.Abs
import Mathlib.RingTheory.PrincipalIdealDomain

/-!
# Statement of Fermat's Last Theorem

This file states Fermat's Last Theorem. We provide a statement over a general semiring with
specific exponent, along with the usual statement over the naturals.

## Main definitions

* `FermatLastTheoremWith R n`: The statement that only solutions to the Fermat
  equation `a^n + b^n = c^n` in the semiring `R` have `a = 0`, `b = 0` or `c = 0`.

  Note that this statement can certainly be false for certain values of `R` and `n`.
  For example `FermatLastTheoremWith ‚Ñù 3` is false as `1^3 + 1^3 = (2^{1/3})^3`, and
  `FermatLastTheoremWith ‚Ñï 2` is false, as 3^2 + 4^2 = 5^2.

* `FermatLastTheoremFor n` : The statement that the only solutions to `a^n + b^n = c^n` in `‚Ñï`
  have `a = 0`, `b = 0` or `c = 0`. Again, this statement is not always true, for
  example `FermatLastTheoremFor 1` is false because `2^1 + 2^1 = 4^1`.

* `FermatLastTheorem` : The statement of Fermat's Last Theorem, namely that the only solutions to
  `a^n + b^n = c^n` in `‚Ñï` when `n ‚â• 3` have `a = 0`, `b = 0` or `c = 0`.

## History

Fermat's Last Theorem was an open problem in number theory for hundreds of years, until it was
finally solved by Andrew Wiles, assisted by Richard Taylor, in 1994 (see
[A. Wiles, *Modular elliptic curves and Fermat's last theorem*][Wiles-FLT] and
[R. Taylor and A. Wiles, *Ring-theoretic properties of certain Hecke algebras*][Taylor-Wiles-FLT]).
An ongoing Lean formalisation of the proof, using mathlib as a dependency, is taking place at
https://github.com/ImperialCollegeLondon/FLT .

-/

open List

/-- Statement of Fermat's Last Theorem over a given semiring with a specific exponent. -/
def FermatLastTheoremWith (R : Type*) [Semiring R] (n : ‚Ñï) : Prop :=
  ‚àÄ a b c : R, a ‚â† 0 ‚Üí b ‚â† 0 ‚Üí c ‚â† 0 ‚Üí a ^ n + b ^ n ‚â† c ^ n

/-- Statement of Fermat's Last Theorem over the naturals for a given exponent. -/
def FermatLastTheoremFor (n : ‚Ñï) : Prop := FermatLastTheoremWith ‚Ñï n

/-- Statement of Fermat's Last Theorem: `a ^ n + b ^ n = c ^ n` has no nontrivial natural solution
when `n ‚â• 3`.

This is now a theorem of Wiles and Taylor--Wiles; see
https://github.com/ImperialCollegeLondon/FLT for an ongoing Lean formalisation of
a proof. -/
def FermatLastTheorem : Prop := ‚àÄ n ‚â• 3, FermatLastTheoremFor n

lemma fermatLastTheoremFor_zero : FermatLastTheoremFor 0 :=
  fun _ _ _ _ _ _ ‚Ü¶ by simp

lemma not_fermatLastTheoremFor_one : ¬¨ FermatLastTheoremFor 1 :=
  fun h ‚Ü¶ h 1 1 2 (by simp) (by simp) (by simp) (by simp)

lemma not_fermatLastTheoremFor_two : ¬¨ FermatLastTheoremFor 2 :=
  fun h ‚Ü¶ h 3 4 5 (by simp) (by simp) (by simp) (by simp)

variable {R : Type*} [Semiring R] [NoZeroDivisors R] {m n : ‚Ñï}

lemma FermatLastTheoremWith.mono (hmn : m ‚à£ n) (hm : FermatLastTheoremWith R m) :
    FermatLastTheoremWith R n := by
  rintro a b c ha hb hc
  obtain ‚ü®k, rfl‚ü© := hmn
  simp_rw [pow_mul']
  refine hm _ _ _ ?_ ?_ ?_ <;> exact pow_ne_zero _ ‚Äπ_‚Ä∫

lemma FermatLastTheoremFor.mono (hmn : m ‚à£ n) (hm : FermatLastTheoremFor m) :
    FermatLastTheoremFor n := by
  exact FermatLastTheoremWith.mono hmn hm

lemma fermatLastTheoremWith_nat_int_rat_tfae (n : ‚Ñï) :
    TFAE [FermatLastTheoremWith ‚Ñï n, FermatLastTheoremWith ‚Ñ§ n, FermatLastTheoremWith ‚Ñö n] := by
  tfae_have 1 ‚Üí 2
  | h, a, b, c, ha, hb, hc, habc => by
    obtain hn | hn := n.even_or_odd
    ¬∑ refine h a.natAbs b.natAbs c.natAbs (by positivity) (by positivity) (by positivity)
        (Int.natCast_inj.1 ?_)
      push_cast
      simp only [hn.pow_abs, habc]
    obtain ha | ha := ha.lt_or_gt <;> obtain hb | hb := hb.lt_or_gt <;>
      obtain hc | hc := hc.lt_or_gt
    ¬∑ refine h a.natAbs b.natAbs c.natAbs (by positivity) (by positivity) (by positivity)
        (Int.natCast_inj.1 ?_)
      push_cast
      simp only [abs_of_neg, neg_pow a, neg_pow b, neg_pow c, ‚Üê mul_add, *]
    ¬∑ exact (by positivity : 0 < c ^ n).not_gt <| habc.symm.trans_lt <| add_neg (hn.pow_neg ha) <|
        hn.pow_neg hb
    ¬∑ refine h b.natAbs c.natAbs a.natAbs (by positivity) (by positivity) (by positivity)
        (Int.natCast_inj.1 ?_)
      push_cast
      simp only [abs_of_pos, abs_of_neg, hn.neg_pow, add_neg_eq_iff_eq_add,
        eq_neg_add_iff_add_eq, *]
    ¬∑ refine h a.natAbs c.natAbs b.natAbs (by positivity) (by positivity) (by positivity)
        (Int.natCast_inj.1 ?_)
      push_cast
      simp only [abs_of_pos, abs_of_neg, hn.neg_pow, neg_add_eq_iff_eq_add,
        *]
    ¬∑ refine h c.natAbs a.natAbs b.natAbs (by positivity) (by positivity) (by positivity)
        (Int.natCast_inj.1 ?_)
      push_cast
      simp only [abs_of_pos, abs_of_neg, hn.neg_pow, neg_add_eq_iff_eq_add,
        eq_add_neg_iff_add_eq, *]
    ¬∑ refine h c.natAbs b.natAbs a.natAbs (by positivity) (by positivity) (by positivity)
        (Int.natCast_inj.1 ?_)
      push_cast
      simp only [abs_of_pos, abs_of_neg, hn.neg_pow, add_neg_eq_iff_eq_add,
        *]
    ¬∑ exact (by positivity : 0 < a ^ n + b ^ n).not_gt <| habc.trans_lt <| hn.pow_neg hc
    ¬∑ refine h a.natAbs b.natAbs c.natAbs (by positivity) (by positivity) (by positivity)
        (Int.natCast_inj.1 ?_)
      push_cast
      simp only [abs_of_pos, *]
  tfae_have 2 ‚Üí 3
  | h, a, b, c, ha, hb, hc, habc => by
    rw [‚Üê Rat.num_ne_zero] at ha hb hc
    refine h (a.num * b.den * c.den) (a.den * b.num * c.den) (a.den * b.den * c.num)
      (by positivity) (by positivity) (by positivity) ?_
    have : (a.den * b.den * c.den : ‚Ñö) ^ n ‚â† 0 := by positivity
    refine Int.cast_injective <| (div_left_inj' this).1 ?_
    push_cast
    simp only [add_div, ‚Üê div_pow, mul_div_mul_comm, div_self (by positivity : (a.den : ‚Ñö) ‚â† 0),
      div_self (by positivity : (b.den : ‚Ñö) ‚â† 0), div_self (by positivity : (c.den : ‚Ñö) ‚â† 0),
      one_mul, mul_one, Rat.num_div_den, habc]
  tfae_have 3 ‚Üí 1
  | h, a, b, c => mod_cast h a b c
  tfae_finish

lemma fermatLastTheoremFor_iff_nat {n : ‚Ñï} : FermatLastTheoremFor n ‚Üî FermatLastTheoremWith ‚Ñï n :=
  Iff.rfl

lemma fermatLastTheoremFor_iff_int {n : ‚Ñï} : FermatLastTheoremFor n ‚Üî FermatLastTheoremWith ‚Ñ§ n :=
  (fermatLastTheoremWith_nat_int_rat_tfae n).out 0 1

lemma fermatLastTheoremFor_iff_rat {n : ‚Ñï} : FermatLastTheoremFor n ‚Üî FermatLastTheoremWith ‚Ñö n :=
  (fermatLastTheoremWith_nat_int_rat_tfae n).out 0 2

/--
A relaxed variant of Fermat's Last Theorem over a given commutative semiring with a specific
exponent, allowing nonzero solutions of units and their common multiples.

1. The variant `FermatLastTheoremWith' R` is weaker than `FermatLastTheoremWith R` in general.
   In particular, it holds trivially for `[Field R]`.
2. This variant is equivalent to the original `FermatLastTheoremWith R` for `R = ‚Ñï` or `‚Ñ§`.
   In general, they are equivalent if there is no solutions of units to the Fermat equation.
3. For a polynomial ring `R = k[X]`, the original `FermatLastTheoremWith R` is false but the weaker
   variant `FermatLastTheoremWith' R` is true. This polynomial variant of Fermat's Last Theorem
   can be shown elementarily using Mason--Stothers theorem.
-/
def FermatLastTheoremWith' (R : Type*) [CommSemiring R] (n : ‚Ñï) : Prop :=
  ‚àÄ a b c : R, a ‚â† 0 ‚Üí b ‚â† 0 ‚Üí c ‚â† 0 ‚Üí a ^ n + b ^ n = c ^ n ‚Üí
    ‚àÉ d a' b' c', (a = a' * d ‚àß b = b' * d ‚àß c = c' * d) ‚àß (IsUnit a' ‚àß IsUnit b' ‚àß IsUnit c')

lemma FermatLastTheoremWith.fermatLastTheoremWith' {R : Type*} [CommSemiring R] {n : ‚Ñï}
    (h : FermatLastTheoremWith R n) : FermatLastTheoremWith' R n :=
  fun a b c _ _ _ _ ‚Ü¶ by exfalso; apply h a b c <;> assumption

lemma fermatLastTheoremWith'_of_semifield (ùïú : Type*) [Semifield ùïú] (n : ‚Ñï) :
    FermatLastTheoremWith' ùïú n := fun a b c ha hb hc _ ‚Ü¶
  ‚ü®1, a, b, c,
    ‚ü®(mul_one a).symm, (mul_one b).symm, (mul_one c).symm‚ü©,
    ‚ü®ha.isUnit, hb.isUnit, hc.isUnit‚ü©‚ü©

@[deprecated (since := "2025-03-21")]
alias fermatLastTheoremWith'_of_field := fermatLastTheoremWith'_of_semifield

lemma FermatLastTheoremWith'.fermatLastTheoremWith {R : Type*} [CommSemiring R] [IsDomain R]
    {n : ‚Ñï} (h : FermatLastTheoremWith' R n)
    (hn : ‚àÄ a b c : R, IsUnit a ‚Üí IsUnit b ‚Üí IsUnit c ‚Üí a ^ n + b ^ n ‚â† c ^ n) :
    FermatLastTheoremWith R n := by
  intro a b c ha hb hc heq
  rcases h a b c ha hb hc heq with ‚ü®d, a', b', c', ‚ü®rfl, rfl, rfl‚ü©, ‚ü®ua, ub, uc‚ü©‚ü©
  rw [mul_pow, mul_pow, mul_pow, ‚Üê add_mul] at heq
  exact hn _ _ _ ua ub uc <| mul_right_cancel‚ÇÄ (pow_ne_zero _ (right_ne_zero_of_mul ha)) heq

lemma fermatLastTheoremWith'_iff_fermatLastTheoremWith {R : Type*} [CommSemiring R] [IsDomain R]
    {n : ‚Ñï} (hn : ‚àÄ a b c : R, IsUnit a ‚Üí IsUnit b ‚Üí IsUnit c ‚Üí a ^ n + b ^ n ‚â† c ^ n) :
    FermatLastTheoremWith' R n ‚Üî FermatLastTheoremWith R n :=
  Iff.intro (fun h ‚Ü¶ h.fermatLastTheoremWith hn) (fun h ‚Ü¶ h.fermatLastTheoremWith')

lemma fermatLastTheoremWith'_nat_int_tfae (n : ‚Ñï) :
    TFAE [FermatLastTheoremFor n, FermatLastTheoremWith' ‚Ñï n, FermatLastTheoremWith' ‚Ñ§ n] := by
  tfae_have 2 ‚Üî 1 := by
    apply fermatLastTheoremWith'_iff_fermatLastTheoremWith
    simp only [Nat.isUnit_iff]
    intro _ _ _ ha hb hc
    rw [ha, hb, hc]
    simp only [one_pow, Nat.reduceAdd, ne_eq, OfNat.ofNat_ne_one, not_false_eq_true]
  tfae_have 3 ‚Üî 1 := by
    rw [fermatLastTheoremFor_iff_int]
    apply fermatLastTheoremWith'_iff_fermatLastTheoremWith
    intro a b c ha hb hc
    by_cases hn : n = 0
    ¬∑ subst hn
      simp only [pow_zero, Int.reduceAdd, ne_eq, OfNat.ofNat_ne_one, not_false_eq_true]
    ¬∑ rw [‚Üê isUnit_pow_iff hn, Int.isUnit_iff] at ha hb hc
      -- case division
      rcases ha with ha | ha <;> rcases hb with hb | hb <;> rcases hc with hc | hc <;>
        rw [ha, hb, hc] <;> decide
  tfae_finish

open Finset in
/-- To prove Fermat Last Theorem in any semiring that is a `NormalizedGCDMonoid` one can assume
that the `gcd` of `{a, b, c}` is `1`. -/
lemma fermatLastTheoremWith_of_fermatLastTheoremWith_coprime {n : ‚Ñï} {R : Type*} [CommSemiring R]
    [IsDomain R] [DecidableEq R] [NormalizedGCDMonoid R]
    (hn : ‚àÄ a b c : R, a ‚â† 0 ‚Üí b ‚â† 0 ‚Üí c ‚â† 0 ‚Üí ({a, b, c} : Finset R).gcd id = 1 ‚Üí
      a ^ n + b ^ n ‚â† c ^ n) :
    FermatLastTheoremWith R n := by
  intro a b c ha hb hc habc
  let s : Finset R := {a, b, c}; let d := s.gcd id
  obtain ‚ü®A, hA‚ü© : d ‚à£ a := gcd_dvd (by simp [s])
  obtain ‚ü®B, hB‚ü© : d ‚à£ b := gcd_dvd (by simp [s])
  obtain ‚ü®C, hC‚ü© : d ‚à£ c := gcd_dvd (by simp [s])
  simp only [hA, hB, hC, mul_ne_zero_iff, mul_pow] at ha hb hc habc
  rw [‚Üê mul_add, mul_right_inj' (pow_ne_zero n ha.1)] at habc
  refine hn A B C ha.2 hb.2 hc.2 ?_ habc
  rw [‚Üê Finset.normalize_gcd, normalize_eq_one]
  obtain ‚ü®u, hu‚ü© := normalize_associated d
  refine ‚ü®u, mul_left_cancel‚ÇÄ (mt normalize_eq_zero.mp ha.1) (hu.symm ‚ñ∏ ?_)‚ü©
  rw [‚Üê Finset.gcd_mul_left, gcd_eq_gcd_image, image_insert, image_insert, image_singleton,
      id_eq, id_eq, id_eq, ‚Üê hA, ‚Üê hB, ‚Üê hC]

lemma dvd_c_of_prime_of_dvd_a_of_dvd_b_of_FLT {n : ‚Ñï} {p : ‚Ñ§} (hp : Prime p) {a b c : ‚Ñ§}
    (hpa : p ‚à£ a) (hpb : p ‚à£ b) (HF : a ^ n + b ^ n + c ^ n = 0) : p ‚à£ c := by
  rcases eq_or_ne n 0 with rfl | hn
  ¬∑ simp at HF
  refine hp.dvd_of_dvd_pow (n := n) (dvd_neg.1 ?_)
  rw [add_eq_zero_iff_eq_neg] at HF
  exact HF.symm ‚ñ∏ dvd_add (dvd_pow hpa hn) (dvd_pow hpb hn)

lemma isCoprime_of_gcd_eq_one_of_FLT {n : ‚Ñï} {a b c : ‚Ñ§} (Hgcd : Finset.gcd {a, b, c} id = 1)
    (HF : a ^ n + b ^ n + c ^ n = 0) : IsCoprime a b := by
  rcases eq_or_ne n 0 with rfl | hn
  ¬∑ simp only [pow_zero, Int.reduceAdd, OfNat.ofNat_ne_zero] at HF
  refine isCoprime_of_prime_dvd ?_ <| (fun p hp hpa hpb ‚Ü¶ hp.not_dvd_one ?_)
  ¬∑ rintro ‚ü®rfl, rfl‚ü©
    simp only [ne_eq, hn, not_false_eq_true, zero_pow, add_zero, zero_add, pow_eq_zero_iff]
      at HF
    simp only [HF, Finset.mem_singleton, Finset.insert_eq_of_mem, Finset.gcd_singleton, id_eq,
      map_zero, zero_ne_one] at Hgcd
  ¬∑ rw [‚Üê Hgcd]
    refine Finset.dvd_gcd_iff.mpr fun x hx ‚Ü¶ ?_
    simp only [Finset.mem_insert, Finset.mem_singleton] at hx
    rcases hx with hx | hx | hx <;> simp only [id_eq, hx, hpa, hpb,
      dvd_c_of_prime_of_dvd_a_of_dvd_b_of_FLT hp hpa hpb HF]
