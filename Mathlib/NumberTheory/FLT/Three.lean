/-
Copyright (c) 2024 Riccardo Brasca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Riccardo Brasca
-/
import Mathlib.NumberTheory.FLT.Basic
import Mathlib.NumberTheory.Cyclotomic.Three

/-!
# Fermat Last Theorem in the case `n = 3`
The goal of this file is to prove Fermat Last theorem in the case `n = 3`.

## Main results
* `fermatLastTheoremThree_case1`: the first case of Fermat Last Theorem when `n = 3`:
  if `a b c : â„¤` are such that `Â¬ 3 âˆ£ a * b * c`, then `a ^ 3 + b ^ 3 â‰  c ^ 3`.

## TODO
Prove case 2.
-/

section case1

open ZMod

private lemma cube_of_castHom_ne_zero {n : ZMod 9} :
    castHom (show 3 âˆ£ 9 by norm_num) (ZMod 3) n â‰  0 â†’ n ^ 3 = 1 âˆ¨ n ^ 3 = 8 := by
  revert n; decide

private lemma cube_of_not_dvd {n : â„¤} (h : Â¬ 3 âˆ£ n) :
    (n : ZMod 9) ^ 3 = 1 âˆ¨ (n : ZMod 9) ^ 3 = 8 := by
  apply cube_of_castHom_ne_zero
  rwa [map_intCast, Ne, ZMod.intCast_zmod_eq_zero_iff_dvd]

/-- If `a b c : â„¤` are such that `Â¬ 3 âˆ£ a * b * c`, then `a ^ 3 + b ^ 3 â‰  c ^ 3`. -/
theorem fermatLastTheoremThree_case_1 {a b c : â„¤} (hdvd : Â¬ 3 âˆ£ a * b * c) :
    a ^ 3 + b ^ 3 â‰  c ^ 3 := by
  simp_rw [Int.prime_three.dvd_mul, not_or] at hdvd
  apply mt (congrArg (Int.cast : â„¤ â†’ ZMod 9))
  simp_rw [Int.cast_add, Int.cast_pow]
  rcases cube_of_not_dvd hdvd.1.1 with ha | ha <;>
  rcases cube_of_not_dvd hdvd.1.2 with hb | hb <;>
  rcases cube_of_not_dvd hdvd.2 with hc | hc <;>
  rw [ha, hb, hc] <;> decide

end case1

section case2

private lemma three_dvd_b_of_dvd_a_of_gcd_eq_one_of_case2 {a b c : â„¤} (ha : a â‰  0)
    (Hgcd: Finset.gcd {a, b, c} id = 1) (h3a : 3 âˆ£ a) (HF : a ^ 3 + b ^ 3 + c ^ 3 = 0)
    (H : âˆ€ a b c : â„¤, c â‰  0 â†’ Â¬ 3 âˆ£ a â†’ Â¬ 3 âˆ£ b  â†’ 3 âˆ£ c â†’ IsCoprime a b â†’ a ^ 3 + b ^ 3 â‰  c ^ 3) :
    3 âˆ£ b := by
  have hbc : IsCoprime (-b) (-c) := by
    refine IsCoprime.neg_neg ?_
    rw [add_comm (a ^ 3), add_assoc, add_comm (a ^ 3), â† add_assoc] at HF
    refine isCoprime_of_gcd_eq_one_of_FLT ?_ HF
    convert Hgcd using 2
    rw [Finset.pair_comm, Finset.Insert.comm]
  by_contra! h3b
  by_cases h3c : 3 âˆ£ c
  Â· apply h3b
    rw [add_assoc, add_comm (b ^ 3), â† add_assoc] at HF
    exact dvd_c_of_prime_of_dvd_a_of_dvd_b_of_FLT Int.prime_three h3a h3c HF
  Â· refine H (-b) (-c) a ha (by simp [h3b]) (by simp [h3c]) h3a hbc ?_
    rw [add_eq_zero_iff_eq_neg, â† (show Odd 3 by decide).neg_pow] at HF
    rw [â† HF]
    ring

open Finset in
private lemma fermatLastTheoremThree_of_dvd_a_of_gcd_eq_one_of_case2 {a b c : â„¤} (ha : a â‰  0)
    (h3a : 3 âˆ£ a) (Hgcd: Finset.gcd {a, b, c} id = 1)
    (H : âˆ€ a b c : â„¤, c â‰  0 â†’ Â¬ 3 âˆ£ a â†’ Â¬ 3 âˆ£ b  â†’ 3 âˆ£ c â†’ IsCoprime a b â†’ a ^ 3 + b ^ 3 â‰  c ^ 3) :
    a ^ 3 + b ^ 3 + c ^ 3 â‰  0 := by
  intro HF
  apply (show Â¬(3 âˆ£ (1 : â„¤)) by decide)
  rw [â† Hgcd]
  refine dvd_gcd (fun x hx â†¦ ?_)
  simp only [mem_insert, mem_singleton] at hx
  have h3b : 3 âˆ£ b := by
    refine three_dvd_b_of_dvd_a_of_gcd_eq_one_of_case2 ha ?_ h3a HF H
    simp only [â† Hgcd, gcd_insert, gcd_singleton, id_eq, â† Int.abs_eq_normalize, abs_neg]
  rcases hx with (hx | hx | hx)
  Â· exact hx â–¸ h3a
  Â· exact hx â–¸ h3b
  Â· simpa [hx] using dvd_c_of_prime_of_dvd_a_of_dvd_b_of_FLT Int.prime_three h3a h3b HF

open Finset Int in
/--
  To prove Fermat's Last Theorem for `n = 3`, it is enough to show that that for all `a`, `b`, `c`
  in `â„¤` such that `c â‰  0`, `Â¬ 3 âˆ£ a`, `Â¬ 3 âˆ£ b`, `a` and `b` are coprime and `3 âˆ£ c`, we have
  `a ^ 3 + b ^ 3 â‰  c ^ 3`.
-/
theorem fermatLastTheoremThree_of_three_dvd_only_c
    (H : âˆ€ a b c : â„¤, c â‰  0 â†’ Â¬ 3 âˆ£ a â†’ Â¬ 3 âˆ£ b  â†’ 3 âˆ£ c â†’ IsCoprime a b â†’ a ^ 3 + b ^ 3 â‰  c ^ 3) :
    FermatLastTheoremFor 3 := by
  rw [fermatLastTheoremFor_iff_int]
  refine fermatLastTheoremWith_of_fermatLastTheoremWith_coprime (fun a b c ha hb hc Hgcd hF â†¦?_)
  by_cases h1 : 3 âˆ£ a * b * c
  swap
  Â· exact fermatLastTheoremThree_case_1 h1 hF
  rw [(prime_three).dvd_mul, (prime_three).dvd_mul] at h1
  rw [â† sub_eq_zero, sub_eq_add_neg, â† (show Odd 3 by decide).neg_pow] at hF
  rcases h1 with ((h3a | h3b) | h3c)
  Â· refine fermatLastTheoremThree_of_dvd_a_of_gcd_eq_one_of_case2 ha h3a ?_ H hF
    simp only [â† Hgcd, Insert.comm, gcd_insert, gcd_singleton, id_eq, â† abs_eq_normalize, abs_neg]
  Â· rw [add_comm (a ^ 3)] at hF
    refine fermatLastTheoremThree_of_dvd_a_of_gcd_eq_one_of_case2 hb h3b ?_ H hF
    simp only [â† Hgcd, Insert.comm, gcd_insert, gcd_singleton, id_eq, â† abs_eq_normalize, abs_neg]
  Â· rw [add_comm _ ((-c) ^ 3), â† add_assoc] at hF
    refine fermatLastTheoremThree_of_dvd_a_of_gcd_eq_one_of_case2 (neg_ne_zero.2 hc) (by simp [h3c])
      ?_ H hF
    rw [Finset.Insert.comm (-c), Finset.pair_comm (-c) b]
    simp only [â† Hgcd, Insert.comm, gcd_insert, gcd_singleton, id_eq, â† abs_eq_normalize, abs_neg]

section eisenstein

open NumberField IsCyclotomicExtension.Rat.Three

variable {K : Type*} [Field K] [NumberField K] [IsCyclotomicExtension {3} â„š K]
variable {Î¶ : K} (hÎ¶ : IsPrimitiveRoot Î¶ (3 : â„•+))

local notation3 "Î·" => (IsPrimitiveRoot.isUnit (hÎ¶.toInteger_isPrimitiveRoot) (by decide)).unit
local notation3 "Î»" => hÎ¶.toInteger - 1

/-- `FermatLastTheoremForThreeGen` is the statement that `a ^ 3 + b ^ 3 = u * c ^ 3` has no
nontrivial solutions in `ğ“ K` for all `u : (ğ“ K)Ë£` such that `Â¬ Î» âˆ£ a`, `Â¬ Î» âˆ£ b` and `Î» âˆ£ c`.
The reason to consider `FermatLastTheoremForThreeGen` is to make a descent argument working. -/
def FermatLastTheoremForThreeGen : Prop :=
  âˆ€ a b c : ğ“ K, âˆ€ u : (ğ“ K)Ë£, c â‰  0 â†’ Â¬ Î» âˆ£ a â†’ Â¬ Î» âˆ£ b  â†’ Î» âˆ£ c â†’ IsCoprime a b â†’
    a ^ 3 + b ^ 3 â‰  u * c ^ 3

/-- To prove `FermatLastTheoremFor 3`, it is enough to prove `FermatLastTheoremForThreeGen`. -/
lemma FermatLastTheoremForThree_of_FermatLastTheoremThreeGen :
    FermatLastTheoremForThreeGen hÎ¶ â†’ FermatLastTheoremFor 3 := by
  intro H
  refine fermatLastTheoremThree_of_three_dvd_only_c (fun a b c hc ha hb âŸ¨x, hxâŸ© hcoprime h â†¦ ?_)
  refine H a b c 1 (by simp [hc]) (fun hdvd â†¦ ha ?_) (fun hdvd â†¦ hb ?_) ?_ ?_ ?_
  Â· rwa [â† Ideal.norm_dvd_iff (hÎ¶.prime_norm_toInteger_sub_one_of_prime_ne_two' (by decide)),
      hÎ¶.norm_toInteger_sub_one_of_prime_ne_two' (by decide)] at hdvd
  Â· rwa [â† Ideal.norm_dvd_iff (hÎ¶.prime_norm_toInteger_sub_one_of_prime_ne_two' (by decide)),
      hÎ¶.norm_toInteger_sub_one_of_prime_ne_two' (by decide)] at hdvd
  Â· exact dvd_trans hÎ¶.toInteger_sub_one_dvd_prime' âŸ¨x, by simp [hx]âŸ©
  Â· rw [show a = algebraMap _ (ğ“ K) a by simp, show b = algebraMap _ (ğ“ K) b by simp]
    exact hcoprime.map _
  Â· simp only [Units.val_one, one_mul]
    exact_mod_cast h

namespace FermatLastTheoremForThreeGen

/-- `Solution'` is a tuple given by a solution to `a ^ 3 + b ^ 3 = u * c ^ 3`,
where `a`, `b`, `c` and `u` are as in `FermatLastTheoremForThreeGen`.
See `Solution` for the actual structure on which we will do the descent. -/
structure Solution' where
  a : ğ“ K
  b : ğ“ K
  c : ğ“ K
  u : (ğ“ K)Ë£
  ha : Â¬ Î» âˆ£ a
  hb : Â¬ Î» âˆ£ b
  hc : c â‰  0
  coprime : IsCoprime a b
  hcdvd : Î» âˆ£ c
  H : a ^ 3 + b ^ 3 = u * c ^ 3
attribute [nolint docBlame] Solution'.a
attribute [nolint docBlame] Solution'.b
attribute [nolint docBlame] Solution'.c
attribute [nolint docBlame] Solution'.u

/-- `Solution` is the same as `Solution'` with the additional assumption that `Î» ^ 2 âˆ£ a + b`. -/
structure Solution extends Solution' hÎ¶ where
  hab : Î» ^ 2 âˆ£ a + b

variable {hÎ¶} (S : Solution hÎ¶) (S' : Solution' hÎ¶) [DecidableRel fun (a b : ğ“ K) â†¦ a âˆ£ b]

/-- For any `S' : Solution'`, the multiplicity of `Î»` in `S'.c` is finite. -/
lemma Solution'.multiplicity_lambda_c_finite :
    multiplicity.Finite (hÎ¶.toInteger - 1) S'.c :=
  multiplicity.finite_of_not_isUnit hÎ¶.zeta_sub_one_prime'.not_unit S'.hc

/-- Given `S' : Solution'`, `S'.multiplicity` is the multiplicity of `Î»` in `S'.c`, as a natural
number. -/
def Solution'.multiplicity :=
  (_root_.multiplicity (hÎ¶.toInteger - 1) S'.c).get (multiplicity_lambda_c_finite S')

/-- Given `S : Solution`, `S.multiplicity` is the multiplicity of `Î»` in `S.c`, as a natural
number. -/
def Solution.multiplicity := S.toSolution'.multiplicity

/-- We say that `S : Solution` is minimal if for all `Sâ‚ : Solution`, the multiplicity of `Î»` in
`S.c` is less or equal than the multiplicity in `Sâ‚.c`. -/
def Solution.isMinimal : Prop := âˆ€ (Sâ‚ : Solution hÎ¶), S.multiplicity â‰¤ Sâ‚.multiplicity

/-- If there is a solution then there is a minimal one. -/
lemma Solution.exists_minimal : âˆƒ (Sâ‚ : Solution hÎ¶), Sâ‚.isMinimal := by
  classical
  let T := {n | âˆƒ (S' : Solution hÎ¶), S'.multiplicity = n}
  rcases Nat.find_spec (âŸ¨S.multiplicity, âŸ¨S, rflâŸ©âŸ© : T.Nonempty) with âŸ¨Sâ‚, hSâ‚âŸ©
  exact âŸ¨Sâ‚, fun S'' â†¦ hSâ‚ â–¸ Nat.find_min' _ âŸ¨S'', rflâŸ©âŸ©

/-- Given `S' : Solution'`, then `S'.a` and `S'.b` are both congruent to `1` modulo `Î» ^ 4` or are
both congruent to `-1`.  -/
lemma a_cube_b_cube_congr_one_or_neg_one :
    Î» ^ 4 âˆ£ S'.a ^ 3 - 1 âˆ§ Î» ^ 4 âˆ£ S'.b ^ 3 + 1 âˆ¨ Î» ^ 4 âˆ£ S'.a ^ 3 + 1 âˆ§ Î» ^ 4 âˆ£ S'.b ^ 3 - 1 := by
  obtain âŸ¨z, hzâŸ© := S'.hcdvd
  rcases lambda_pow_four_dvd_cube_sub_one_or_add_one_of_lambda_not_dvd hÎ¶ S'.ha with
    (âŸ¨x, hxâŸ© | âŸ¨x, hxâŸ©) <;>
  rcases lambda_pow_four_dvd_cube_sub_one_or_add_one_of_lambda_not_dvd hÎ¶ S'.hb with
    (âŸ¨y, hyâŸ© | âŸ¨y, hyâŸ©)
  Â· exfalso
    replace hÎ¶ : IsPrimitiveRoot Î¶ ((3 : â„•+) ^ 1) := by rwa [pow_one]
    refine hÎ¶.toInteger_sub_one_not_dvd_two (by decide) âŸ¨S'.u * Î» ^ 2 * z ^ 3 - Î» ^ 3 * (x + y), ?_âŸ©
    symm
    calc _ = S'.u * (Î» * z) ^ 3 - Î» ^ 4 * x - Î» ^ 4 * y := by ring
    _ = (S'.a ^ 3 + S'.b ^ 3) - (S'.a ^ 3 - 1) - (S'.b ^ 3 - 1) := by rw [â† hx, â† hy, â† hz, â† S'.H]
    _ = 2 := by ring
  Â· left
    exact âŸ¨âŸ¨x, hxâŸ©, âŸ¨y, hyâŸ©âŸ©
  Â· right
    exact âŸ¨âŸ¨x, hxâŸ©, âŸ¨y, hyâŸ©âŸ©
  Â· exfalso
    replace hÎ¶ : IsPrimitiveRoot Î¶ ((3 : â„•+) ^ 1) := by rwa [pow_one]
    refine hÎ¶.toInteger_sub_one_not_dvd_two (by decide) âŸ¨Î» ^ 3 * (x + y) - S'.u * Î» ^ 2 * z ^ 3, ?_âŸ©
    symm
    calc _ =  Î» ^ 4 * x + Î» ^ 4 * y - S'.u * (Î» * z) ^ 3 := by ring
    _ = (S'.a ^ 3 + 1) + (S'.b ^ 3 + 1) - (S'.a ^ 3 + S'.b ^ 3) := by rw [â† hx, â† hy, â† hz, â† S'.H]
    _ = 2 := by ring

/-- Given `S' : Solution'`, we have that `Î» ^ 4` divides `S'.c ^ 3`. -/
lemma lambda_pow_four_dvd_c_cube : Î» ^ 4 âˆ£ S'.c ^ 3 := by
  rcases a_cube_b_cube_congr_one_or_neg_one S' with
    (âŸ¨âŸ¨x, hxâŸ©, âŸ¨y, hyâŸ©âŸ© | âŸ¨âŸ¨x, hxâŸ©, âŸ¨y, hyâŸ©âŸ©) <;>
  Â· refine âŸ¨S'.uâ»Â¹ * (x + y), ?_âŸ©
    symm
    calc _ = S'.uâ»Â¹ * (Î» ^ 4 * x + Î» ^ 4 * y) := by ring
    _ = S'.uâ»Â¹ * (S'.a ^ 3 + S'.b ^ 3) := by rw [â† hx, â† hy]; ring
    _ = S'.uâ»Â¹ * (S'.u * S'.c ^ 3) := by rw [S'.H]
    _ = S'.c ^ 3 := by simp

/-- Given `S' : Solution'`, we have that `Î» ^ 2` divides `S'.c`. -/
lemma lambda_sq_dvd_c : Î» ^ 2 âˆ£ S'.c := by
  have hm := S'.multiplicity_lambda_c_finite
  suffices 2 â‰¤ (multiplicity ((hÎ¶.toInteger - 1)) S'.c).get hm by
    obtain âŸ¨x, hxâŸ© := multiplicity.pow_multiplicity_dvd hm
    refine âŸ¨Î» ^ ((multiplicity ((hÎ¶.toInteger - 1)) S'.c).get hm - 2) * x, ?_âŸ©
    rw [â† mul_assoc, â† pow_add]
    convert hx using 3
    simp [this]
  have := lambda_pow_four_dvd_c_cube S'
  have hm1 : (multiplicity (hÎ¶.toInteger - 1) (S'.c ^ 3)).get
    (multiplicity.finite_pow hÎ¶.zeta_sub_one_prime' hm) =
    multiplicity (hÎ¶.toInteger - 1) (S'.c ^ 3) := by simp
  rw [multiplicity.pow_dvd_iff_le_multiplicity, â† hm1, multiplicity.pow' hÎ¶.zeta_sub_one_prime' hm,
    Nat.cast_ofNat, Nat.ofNat_le_cast] at this
  omega

/-- Given `S' : Solution'`, we have that `2 â‰¤ S'.multiplicity`. -/
lemma Solution'.two_le_multiplicity : 2 â‰¤ S'.multiplicity := by
  simpa [â† PartENat.coe_le_coe, Solution'.multiplicity] using
    multiplicity.le_multiplicity_of_pow_dvd (lambda_sq_dvd_c S')

/-- Given `S : Solution`, we have that `2 â‰¤ S.multiplicity`. -/
lemma Solution.two_le_multiplicity : 2 â‰¤ S.multiplicity :=
  S.toSolution'.two_le_multiplicity

/-- Given `S' : Solution'`, the key factorization of `S'.a ^ 3 + S'.b ^ 3`. -/
lemma a_cube_add_b_cube_eq_mul :
    S'.a ^ 3 + S'.b ^ 3 = (S'.a + S'.b) * (S'.a + Î· * S'.b) * (S'.a + Î· ^ 2 * S'.b) := by
  have := hÎ¶.isRoot_cyclotomic (by decide)
  simp only [PNat.val_ofNat, Polynomial.cyclotomic_three, Polynomial.IsRoot.def,
    Polynomial.eval_add, Polynomial.eval_pow, Polynomial.eval_X, Polynomial.eval_one] at this
  symm
  calc _ = S'.a^3+S'.a^2*S'.b*(Î·^2+Î·+1)+S'.a*S'.b^2*(Î·^2+Î·+Î·^3)+Î·^3*S'.b^3 := by ring
  _ = S'.a^3+S'.a^2*S'.b*(Î·^2+Î·+1)+S'.a*S'.b^2*(Î·^2+Î·+1)+S'.b^3 := by
    simp [hÎ¶.toInteger_cube_eq_one]
  _ = S'.a ^ 3 + S'.b ^ 3 := by ext; simp [this]

open PartENat in
/-- Given `S' : Solution'`, we have that `Î» ^ 2` divides one amongst `S'.a + S'.b`,
`S'.a + Î· * S'.b` and `S'.a + Î· ^ 2 * S'.b`. -/
lemma lambda_sq_dvd_or_dvd_or_dvd :
    Î» ^ 2 âˆ£ S'.a + S'.b âˆ¨ Î» ^ 2 âˆ£ S'.a + Î· * S'.b âˆ¨ Î» ^ 2 âˆ£ S'.a + Î· ^ 2 * S'.b := by
  by_contra! h
  rcases h with âŸ¨h1, h2, h3âŸ©
  rw [â† multiplicity.multiplicity_lt_iff_not_dvd] at h1 h2 h3
  have h1' : multiplicity.Finite (hÎ¶.toInteger - 1) (S'.a + S'.b) :=
    multiplicity.ne_top_iff_finite.1 (fun ht â†¦ by simp [ht] at h1)
  have h2' : multiplicity.Finite (hÎ¶.toInteger - 1) (S'.a + Î· * S'.b) := by
    refine multiplicity.ne_top_iff_finite.1 (fun ht â†¦ ?_)
    rw [coe_eta] at ht
    simp [ht] at h2
  have h3' : multiplicity.Finite (hÎ¶.toInteger - 1) (S'.a + Î· ^ 2 * S'.b) := by
    refine multiplicity.ne_top_iff_finite.1 (fun ht â†¦ ?_)
    rw [coe_eta] at ht
    simp [ht] at h3
  replace h1' : (multiplicity (hÎ¶.toInteger - 1) (S'.a + S'.b)).get h1' =
    multiplicity (hÎ¶.toInteger - 1) (S'.a + S'.b) := by simp
  replace h2' : (multiplicity (hÎ¶.toInteger - 1) (S'.a + Î· * S'.b)).get h2' =
    multiplicity (hÎ¶.toInteger - 1) (S'.a + Î· * S'.b) := by simp
  replace h3' : (multiplicity (hÎ¶.toInteger - 1) (S'.a + Î· ^ 2 * S'.b)).get h3' =
    multiplicity (hÎ¶.toInteger - 1) (S'.a + Î· ^ 2 * S'.b) := by simp
  rw [â† h1', coe_lt_coe] at h1; rw [â† h2', coe_lt_coe] at h2; rw [â† h3', coe_lt_coe] at h3
  have := (pow_dvd_pow_of_dvd (lambda_sq_dvd_c S') 3).mul_left S'.u
  rw [â† pow_mul, â† S'.H, a_cube_add_b_cube_eq_mul, multiplicity.pow_dvd_iff_le_multiplicity,
    multiplicity.mul hÎ¶.zeta_sub_one_prime', multiplicity.mul hÎ¶.zeta_sub_one_prime', â† h1', â† h2',
    â† h3', â† Nat.cast_add, â† Nat.cast_add, coe_le_coe] at this
  omega

open Units in
/-- Given `S' : Solution'`, we may assume that `Î» ^ 2` divides `S'.a + S'.b âˆ¨ Î» ^ 2` (see also the
result below). -/
lemma ex_cube_add_cube_eq_and_isCoprime_and_not_dvd_and_dvd :
    âˆƒ (a' b' : ğ“ K), a' ^ 3 + b' ^ 3 = S'.u * S'.c ^ 3 âˆ§ IsCoprime a' b' âˆ§ Â¬ Î» âˆ£ a' âˆ§
      Â¬ Î» âˆ£ b' âˆ§ Î» ^ 2 âˆ£ a' + b' := by
  rcases lambda_sq_dvd_or_dvd_or_dvd S' with (h | h | h)
  Â· exact âŸ¨S'.a, S'.b, S'.H, S'.coprime, S'.ha, S'.hb, hâŸ©
  Â· refine âŸ¨S'.a, Î· * S'.b, ?_, ?_, S'.ha, fun âŸ¨x, hxâŸ© â†¦ S'.hb âŸ¨Î· ^ 2 * x, ?_âŸ©, hâŸ©
    Â· simp [mul_pow, â† val_pow_eq_pow_val, hÎ¶.toInteger_cube_eq_one, val_one, one_mul, S'.H]
    Â· refine (isCoprime_mul_unit_left_right (Units.isUnit Î·) _ _).2 S'.coprime
    Â· rw [mul_comm _ x, â† mul_assoc, â† hx, mul_comm _ S'.b, mul_assoc, â† pow_succ', coe_eta,
        hÎ¶.toInteger_cube_eq_one, mul_one]
  Â· refine âŸ¨S'.a, Î· ^ 2 * S'.b, ?_, ?_, S'.ha, fun âŸ¨x, hxâŸ© â†¦ S'.hb âŸ¨Î· * x, ?_âŸ©, hâŸ©
    Â· rw [mul_pow, â† pow_mul, mul_comm 2, pow_mul, coe_eta, hÎ¶.toInteger_cube_eq_one, one_pow,
        one_mul, S'.H]
    Â· exact (isCoprime_mul_unit_left_right ((Units.isUnit Î·).pow _) _ _).2 S'.coprime
    Â· rw [mul_comm _ x, â† mul_assoc, â† hx, mul_comm _ S'.b, mul_assoc, â† pow_succ, coe_eta,
        hÎ¶.toInteger_cube_eq_one, mul_one]

/-- Given `S' : Solution'`, then there is `Sâ‚ : Solution` such that
`Sâ‚.multiplicity = S'.multiplicity`. -/
lemma exists_Solution_of_Solution' : âˆƒ (Sâ‚ : Solution hÎ¶), Sâ‚.multiplicity = S'.multiplicity := by
  obtain âŸ¨a, b, H, coprime, ha, hb, habâŸ© := ex_cube_add_cube_eq_and_isCoprime_and_not_dvd_and_dvd S'
  exact âŸ¨
  { a := a
    b := b
    c := S'.c
    u := S'.u
    ha := ha
    hb := hb
    hc := S'.hc
    coprime := coprime
    hcdvd := S'.hcdvd
    H := H
    hab := hab }, rflâŸ©

end FermatLastTheoremForThreeGen

end eisenstein

end case2
