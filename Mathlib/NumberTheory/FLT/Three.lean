/-
Copyright (c) 2024 Riccardo Brasca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Riccardo Brasca
-/
import Mathlib.NumberTheory.Cyclotomic.Three
import Mathlib.NumberTheory.Cyclotomic.PID
import Mathlib.NumberTheory.FLT.Basic

/-!
# Fermat Last Theorem in the case `n = 3`
The goal of this file is to prove Fermat Last theorem in the case `n = 3`.

## Main results
* `fermatLastTheoremThree_case_1`: the first case of Fermat Last Theorem when `n = 3`:
  if `a b c : â„•` are such that `Â¬ 3 âˆ£ a * b * c`, then `a ^ 3 + b ^ 3 â‰  c ^ 3`.

## TODO
Prove case 2.
-/

open NumberField Units InfinitePlace nonZeroDivisors Polynomial IsCyclotomicExtension.Rat.Three

section case1

open ZMod

private lemma cube_of_castHom_ne_zero {n : ZMod 9} :
    castHom (show 3 âˆ£ 9 by norm_num) (ZMod 3) n â‰  0 â†’ n ^ 3 = 1 âˆ¨ n ^ 3 = 8 := by
  fin_cases n <;> decide

private lemma cube_of_not_dvd {n : â„•} (h : Â¬ 3 âˆ£ n) :
    (n : ZMod 9) ^ 3 = 1 âˆ¨ (n : ZMod 9) ^ 3 = 8 := by
  apply cube_of_castHom_ne_zero
  rwa [map_natCast, Ne, Fin.nat_cast_eq_zero]

/--If `a b c : â„•` are such that `Â¬ 3 âˆ£ a * b * c`, then `a ^ 3 + b ^ 3 â‰  c ^ 3`. -/
theorem fermatLastTheoremThree_case_1 {a b c : â„•} (hdvd : Â¬ 3 âˆ£ a * b * c) :
    a ^ 3 + b ^ 3 â‰  c ^ 3 := by
  simp_rw [Nat.prime_three.dvd_mul, not_or] at hdvd
  apply mt (congrArg (Nat.cast : â„• â†’ ZMod 9))
  simp_rw [Nat.cast_add, Nat.cast_pow]
  rcases cube_of_not_dvd hdvd.1.1 with ha | ha <;>
  rcases cube_of_not_dvd hdvd.1.2 with hb | hb <;>
  rcases cube_of_not_dvd hdvd.2 with hc | hc <;>
  rw [ha, hb, hc] <;> decide

end case1

section case2

section misc

/-- To prove `FermatLastTheoremFor 3`, we may assume that `3 âˆ£ c`. -/
theorem fermatLastTheoremThree_of_three_dvd_c
    (H : âˆ€ a b c : â„¤, a â‰  0 â†’ b â‰  0 â†’ c â‰  0 â†’ 3 âˆ£ c â†’ a ^ 3 + b ^ 3 â‰  c ^ 3) :
    FermatLastTheoremFor 3 := by
  intro a b c ha hb hc habc
  by_cases h1 : 3 âˆ£ a * b * c
  swap
  Â· exact fermatLastTheoremThree_case_1 h1 habc
  rw [Nat.Prime.dvd_mul (Nat.prime_three), Nat.Prime.dvd_mul (Nat.prime_three)] at h1
  rcases h1 with ((âŸ¨k, hkâŸ© | âŸ¨k, hkâŸ©) | âŸ¨k, hkâŸ©)
  Â· refine H (-(c : â„¤)) b (-(a : â„¤)) (by simp [hc]) (by simp [hb]) (by simp [ha]) ?_ ?_
    Â· exact âŸ¨-k, by simp [hk]âŸ©
    Â· rw [Odd.neg_pow (by decide), Odd.neg_pow (by decide), add_comm, â† sub_eq_add_neg,
        sub_eq_iff_eq_add, add_comm, â† sub_eq_add_neg, eq_sub_iff_add_eq, add_comm]
      exact_mod_cast habc
  Â· refine H a (-(c : â„¤)) ((-(b : â„¤))) (by simp [ha]) (by simp [hc]) (by simp [hb]) ?_ ?_
    Â· exact âŸ¨-k, by simp [hk]âŸ©
    Â· rw [Odd.neg_pow (by decide), Odd.neg_pow (by decide), â† sub_eq_add_neg, sub_eq_iff_eq_add,
        add_comm, â† sub_eq_add_neg, eq_sub_iff_add_eq]
      exact_mod_cast habc
  Â· refine H a b c (by simp [ha]) (by simp [hb]) (by simp [hc]) ?_ ?_
    Â· exact âŸ¨k, by simp [hk]âŸ©
    Â· exact_mod_cast habc

lemma three_dvd_gcd_of_dvd_a_of_dvd_c {a b c : â„•} (ha : 3 âˆ£ a) (hc : 3 âˆ£ c)
    (hF : a ^ 3 + b ^ 3 = c ^ 3) : 3 âˆ£ ({a, b, c} : Finset â„•).gcd id := by
  have hb : 3 âˆ£ b := by
    have : 3 âˆ£ (b : â„¤) ^ 3 := by
      replace hF : (a : â„¤) ^ 3 + (b : â„¤) ^ 3 = (c : â„¤) ^ 3 := by exact_mod_cast hF
      rw [add_comm, â† eq_sub_iff_add_eq] at hF
      rw [hF]
      exact dvd_sub (dvd_pow (by exact_mod_cast hc) (by decide))
        (dvd_pow (by exact_mod_cast ha) (by decide))
    exact Int.coe_nat_dvd.1 <| Int.prime_three.dvd_of_dvd_pow this
  refine Finset.dvd_gcd (fun x hx â†¦ ?_)
  simp only [Finset.mem_insert, Finset.mem_singleton] at hx
  rcases hx with (hx | hx | hx)
  Â· exact hx â–¸ ha
  Â· exact hx â–¸ hb
  Â· exact hx â–¸ hc

lemma three_dvd_gcd_of_dvd_a_of_dvd_b {a b c : â„•} (ha : 3 âˆ£ a) (hb : 3 âˆ£ b)
    (hF : a ^ 3 + b ^ 3 = c ^ 3) : 3 âˆ£ ({a, b, c} : Finset â„•).gcd id := by
  have hc : 3 âˆ£ c := by
    have : 3 âˆ£ (c : â„¤) ^ 3 := by
      replace hF : (a : â„¤) ^ 3 + (b : â„¤) ^ 3 = (c : â„¤) ^ 3 := by exact_mod_cast hF
      rw [â† hF]
      exact dvd_add (dvd_pow (by exact_mod_cast ha) (by decide))
        (dvd_pow (by exact_mod_cast hb) (by decide))
    exact Int.coe_nat_dvd.1 <| Int.prime_three.dvd_of_dvd_pow this
  refine Finset.dvd_gcd (fun x hx â†¦ ?_)
  simp only [Finset.mem_insert, Finset.mem_singleton] at hx
  rcases hx with (hx | hx | hx)
  Â· exact hx â–¸ ha
  Â· exact hx â–¸ hb
  Â· exact hx â–¸ hc

lemma three_dvd_gcd_of_dvd_b_of_dvd_c {a b c : â„•} (hb : 3 âˆ£ b) (hc : 3 âˆ£ c)
    (hF : a ^ 3 + b ^ 3 = c ^ 3) : 3 âˆ£ ({a, b, c} : Finset â„•).gcd id := by
  have ha : 3 âˆ£ a := by
    have : 3 âˆ£ (a : â„¤) ^ 3 := by
      replace hF : (a : â„¤) ^ 3 + (b : â„¤) ^ 3 = (c : â„¤) ^ 3 := by exact_mod_cast hF
      rw [â† eq_sub_iff_add_eq] at hF
      rw [hF]
      exact dvd_sub (dvd_pow (by exact_mod_cast hc) (by decide))
            (dvd_pow (by exact_mod_cast hb) (by decide))
    exact Int.coe_nat_dvd.1 <| Int.prime_three.dvd_of_dvd_pow this
  refine Finset.dvd_gcd (fun x hx â†¦ ?_)
  simp only [Finset.mem_insert, Finset.mem_singleton] at hx
  rcases hx with (hx | hx | hx)
  Â· exact hx â–¸ ha
  Â· exact hx â–¸ hb
  Â· exact hx â–¸ hc

/-- To prove `FermatLastTheoremFor 3`, we may assume that `Â¬ 3 âˆ£ a`, `Â¬ 3 âˆ£ b` and `3 âˆ£ c`. -/
theorem fermatLastTheoremThree_of_three_dvd_only_c
    (H : âˆ€ a b c : â„¤, c â‰  0 â†’ Â¬ 3 âˆ£ a â†’ Â¬ 3 âˆ£ b  â†’ 3 âˆ£ c â†’ a ^ 3 + b ^ 3 â‰  c ^ 3) :
    FermatLastTheoremFor 3 := by
  apply FermatLastTheoremFor_of_FermatLastTheoremFor_coprime
  intro a b c ha hb hc Hgcd hF
  by_cases h1 : 3 âˆ£ a * b * c
  swap
  Â· exact fermatLastTheoremThree_case_1 h1 hF
  rw [Nat.Prime.dvd_mul (Nat.prime_three), Nat.Prime.dvd_mul (Nat.prime_three)] at h1
  have h3 : Â¬(3 âˆ£ 1) := by decide
  rcases h1 with ((âŸ¨k, hkâŸ© | âŸ¨k, hkâŸ©) | âŸ¨k, hkâŸ©)
  Â· refine H (-(c : â„¤)) b (-(a : â„¤)) (by simp [ha]) (fun hdvd â†¦ h3 ?_) (fun hdvd â†¦ h3 ?_) ?_ ?_
    Â· rw [â† Hgcd]
      exact three_dvd_gcd_of_dvd_a_of_dvd_c âŸ¨k, hkâŸ© (Int.coe_nat_dvd.1 (dvd_neg.1 hdvd)) hF
    Â· rw [â† Hgcd]
      exact three_dvd_gcd_of_dvd_a_of_dvd_b âŸ¨k, hkâŸ© (by exact_mod_cast hdvd) hF
    Â· exact âŸ¨-k, by simp [hk]âŸ©
    Â· rw [Odd.neg_pow (by decide), Odd.neg_pow (by decide), add_comm, â† sub_eq_add_neg,
        sub_eq_iff_eq_add, add_comm, â† sub_eq_add_neg, eq_sub_iff_add_eq, add_comm]
      exact_mod_cast hF
  Â· refine H a (-(c : â„¤)) ((-(b : â„¤))) (by simp [hb]) (fun hdvd â†¦ h3 ?_) (fun hdvd â†¦ h3 ?_) ?_ ?_
    Â· rw [â† Hgcd]
      exact three_dvd_gcd_of_dvd_a_of_dvd_b (by exact_mod_cast hdvd) âŸ¨k, hkâŸ© hF
    Â· rw [â† Hgcd]
      exact three_dvd_gcd_of_dvd_b_of_dvd_c âŸ¨k, hkâŸ© (Int.coe_nat_dvd.1 (dvd_neg.1 hdvd)) hF
    Â· exact âŸ¨-k, by simp [hk]âŸ©
    Â· rw [Odd.neg_pow (by decide), Odd.neg_pow (by decide), â† sub_eq_add_neg, sub_eq_iff_eq_add,
        add_comm, â† sub_eq_add_neg, eq_sub_iff_add_eq]
      exact_mod_cast hF
  Â· refine H a b c (by simp [hc]) (fun hdvd â†¦ h3 ?_) (fun hdvd â†¦ h3 ?_) ?_ ?_
    Â· rw [â† Hgcd]
      exact three_dvd_gcd_of_dvd_a_of_dvd_c (by exact_mod_cast hdvd) âŸ¨k, hkâŸ© hF
    Â· rw [â† Hgcd]
      exact three_dvd_gcd_of_dvd_b_of_dvd_c (by exact_mod_cast hdvd) âŸ¨k, hkâŸ© hF
    Â· exact âŸ¨k, by simp [hk]âŸ©
    Â· exact_mod_cast hF

end misc

section eisenstein

variable {K : Type*} [Field K] [NumberField K] [IsCyclotomicExtension {3} â„š K]
variable {Î¶ : K} (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(3 : â„•+)) (u : (ğ“ K)Ë£)

local notation3 "Î·" => hÎ¶.toInteger

local notation3 "Î»" => hÎ¶.toInteger - 1

/-- Let `K` be a `3`-rd cyclotomic extension of `â„š` and let `Î¶ : K` be such that
`hÎ¶ : IsPrimitiveRoot Î¶ 3`. Setting `Î» = Î¶ - 1` (in `ğ“ K`), `FermatLastTheoremForThreeGen hÎ¶`
is the statement that `a ^ 3 + b ^ 3 = u * c ^ 3` has no nontrivial solutions in `ğ“ K` for all
`u : (ğ“ K)Ë£` such that `Â¬ Î» âˆ£ a`, `Â¬ Î» âˆ£ b` and `Î» âˆ£ c`.
The reason to consider `FermatLastTheoremForThreeGen hÎ¶` is to make a descent argument working. -/
def FermatLastTheoremForThreeGen : Prop :=
  âˆ€ a b c : ğ“ K, âˆ€ u : (ğ“ K)Ë£, c â‰  0 â†’ Â¬ Î» âˆ£ a â†’ Â¬ Î» âˆ£ b  â†’ Î» âˆ£ c â†’ a ^ 3 + b ^ 3 â‰  u * c ^ 3

/-- To prove `FermatLastTheoremFor 3`, it is enough to prove `FermatLastTheoremForThreeGen hÎ¶`. -/
lemma FermatLastTheoremForThree_of_FermatLastTheoremThreeGen :
    FermatLastTheoremForThreeGen hÎ¶ â†’ FermatLastTheoremFor 3 := by
  intro H
  refine fermatLastTheoremThree_of_three_dvd_only_c (fun a b c hc ha hb âŸ¨x, hxâŸ© h â†¦ ?_)
  refine H a b c 1 (by simp [hc]) (fun hdvd â†¦ ha ?_) (fun hdvd â†¦ hb ?_) ?_ ?_
  Â· rwa [â† Ideal.norm_dvd_iff (norm_lambda_prime hÎ¶), norm_lambda hÎ¶] at hdvd
  Â· rwa [â† Ideal.norm_dvd_iff (norm_lambda_prime hÎ¶), norm_lambda hÎ¶] at hdvd
  Â· exact dvd_trans (lambda_dvd_three hÎ¶) âŸ¨x, by simp [hx]âŸ©
  Â· simp only [val_one, one_mul]
    exact_mod_cast h

section FermatLastTheoremForThreeGen

variable {a b c : ğ“ K} {u : (ğ“ K)Ë£} (hc : c â‰  0) (H : a ^ 3 + b ^ 3 = u * c ^ 3)

lemma a_cube_b_cube_same_congr (ha : Â¬ Î» âˆ£ a) (hb : Â¬ Î» âˆ£ b) (hcdvd : Î» âˆ£ c) :
    Î» ^ 4 âˆ£ a ^ 3 - 1 âˆ§ Î» ^ 4 âˆ£ b ^ 3 + 1 âˆ¨  Î» ^ 4 âˆ£ a ^ 3 + 1 âˆ§ Î» ^ 4 âˆ£ b ^ 3 - 1 := by
  obtain âŸ¨z, hzâŸ© := hcdvd
  rcases lambda_pow_four_dvd_cube_sub_one_or_add_one_of_lambda_not_dvd hÎ¶ ha with
    (âŸ¨x, hxâŸ© | âŸ¨x, hxâŸ©) <;>
  rcases lambda_pow_four_dvd_cube_sub_one_or_add_one_of_lambda_not_dvd hÎ¶ hb with
    (âŸ¨y, hyâŸ© | âŸ¨y, hyâŸ©)
  Â· exfalso
    refine lambda_not_dvd_two hÎ¶ âŸ¨u * Î» ^ 2 * z ^ 3 - Î» ^ 3 * (x + y), ?_âŸ©
    symm
    calc _ = u * (Î» * z) ^ 3 - Î» ^ 4 * x - Î» ^ 4 * y := by ring
    _ = (a ^ 3 + b ^ 3) - (a ^ 3 - 1) - (b ^ 3 - 1) := by rw [â† hx, â† hy, â† hz, â† H]
    _ = 2 := by ring
  Â· left
    exact âŸ¨âŸ¨x, hxâŸ©, âŸ¨y, hyâŸ©âŸ©
  Â· right
    exact âŸ¨âŸ¨x, hxâŸ©, âŸ¨y, hyâŸ©âŸ©
  Â· exfalso
    refine lambda_not_dvd_two hÎ¶ âŸ¨Î» ^ 3 * (x + y) - u * Î» ^ 2 * z ^ 3, ?_âŸ©
    symm
    calc _ =  Î» ^ 4 * x + Î» ^ 4 * y - u * (Î» * z) ^ 3 := by ring
    _ = (a ^ 3 + 1) + (b ^ 3 + 1) - (a ^ 3 + b ^ 3) := by rw [â† hx, â† hy, â† hz, â† H]
    _ = 2 := by ring

lemma lambda_pow_four_dvd_c_cube (ha : Â¬ Î» âˆ£ a) (hb : Â¬ Î» âˆ£ b) (hcdvd : Î» âˆ£ c) :
    Î» ^ 4 âˆ£ c ^ 3 := by
  rcases a_cube_b_cube_same_congr hÎ¶ H ha hb hcdvd with
    (âŸ¨âŸ¨x, hxâŸ©, âŸ¨y, hyâŸ©âŸ© | âŸ¨âŸ¨x, hxâŸ©, âŸ¨y, hyâŸ©âŸ©) <;> {
  refine âŸ¨uâ»Â¹ * (x + y), ?_âŸ©
  symm
  calc _ = uâ»Â¹ * (Î» ^ 4 * x + Î» ^ 4 * y) := by ring
  _ = uâ»Â¹ * (a ^ 3 + b ^ 3) := by rw [â† hx, â† hy]; ring
  _ = uâ»Â¹ * (u * c ^ 3) := by rw [H]
  _ = c ^ 3 := by simp }

lemma lambda_pow_two_dvd_c (ha : Â¬ Î» âˆ£ a) (hb : Â¬ Î» âˆ£ b) (hcdvd : Î» âˆ£ c) :
    Î» ^ 2 âˆ£ c := by
  classical
  have hm : multiplicity.Finite (hÎ¶.toInteger - 1) c := by
    have := IsCyclotomicExtension.Rat.three_pid K
    refine multiplicity.finite_of_not_isUnit (lambda_not_unit hÎ¶) hc
  suffices 2 â‰¤ (multiplicity ((hÎ¶.toInteger - 1)) c).get hm by
    Â· obtain âŸ¨x, hxâŸ© := multiplicity.pow_multiplicity_dvd hm
      refine âŸ¨Î» ^ ((multiplicity ((hÎ¶.toInteger - 1)) c).get hm - 2) * x, ?_âŸ©
      rw [â† mul_assoc, â† pow_add]
      convert hx using 3
      simp [this]
  have := lambda_pow_four_dvd_c_cube hÎ¶ H ha hb hcdvd
  have hm1 :(multiplicity (hÎ¶.toInteger - 1) (c ^ 3)).get
    (multiplicity.finite_pow hÎ¶.zeta_sub_one_prime' hm) =
    multiplicity (hÎ¶.toInteger - 1) (c ^ 3) := by simp
  rw [multiplicity.pow_dvd_iff_le_multiplicity, â† hm1, multiplicity.pow' hÎ¶.zeta_sub_one_prime' hm,
    Nat.cast_ofNat, Nat.ofNat_le_cast] at this
  linarith

theorem final (hc : c â‰  0) (ha : Â¬ Î» âˆ£ a) (hb : Â¬ Î» âˆ£ b) (hcdvd : Î» âˆ£ c)
    (H : a ^ 3 + b ^ 3 = u * c ^ 3) : False := by
  sorry

end FermatLastTheoremForThreeGen

end eisenstein

end case2

theorem fermatLastTheoremThree : FermatLastTheoremFor 3 := by
  let K := CyclotomicField 3 â„š
  have hÎ¶ := IsCyclotomicExtension.zeta_spec 3 â„š (CyclotomicField 3 â„š)
  have : NumberField K := IsCyclotomicExtension.numberField {3} â„š _
  apply FermatLastTheoremForThree_of_FermatLastTheoremThreeGen hÎ¶
  intro a b c u hc ha hb hcdvd H
  exact final hÎ¶ hc ha hb hcdvd H
