/-
Copyright (c) 2024 Riccardo Brasca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Riccardo Brasca, Sanyam Gupta, Omar Haddad, David Lowry-Duda,
  Lorenzo Luccioli, Pietro Monticone, Alexis Saurin, Florent Schaffhauser
-/
import Mathlib.NumberTheory.FLT.Basic
import Mathlib.NumberTheory.Cyclotomic.PID
import Mathlib.NumberTheory.Cyclotomic.Three

/-!
# Fermat Last Theorem in the case `n = 3`
The goal of this file is to prove Fermat Last theorem in the case `n = 3`.

## Main results
* `fermatLastTheoremThree_case1`: the first case of Fermat Last Theorem when `n = 3`:
  if `a b c : ‚Ñ§` are such that `¬¨ 3 ‚à£ a * b * c`, then `a ^ 3 + b ^ 3 ‚â† c ^ 3`.

## TODO
Prove case 2.

## Implementation details
We follow the proof in <https://webusers.imj-prg.fr/~marc.hindry/Cours-arith.pdf>, page 43. The
strategy is the following:
* Case 1 is completely elementary and is proved using congruences modulo `9`.
* To prove case 2, we consider the generalized equation `a ^ 3 + b ^ 3 = u * c ^ 3`, where `a`, `b`,
  and `c` are in the cyclotomic ring `‚Ñ§[Œ∂‚ÇÉ]` (where `Œ∂‚ÇÉ` is a primitive cube root of unity) and `u`
  is a unit of `‚Ñ§[Œ∂‚ÇÉ]`. `FermatLastTheoremForThree_of_FermatLastTheoremThreeGen` (whose proof is
  rather elementary on paper) says that to prove Fermat's last theorem for exponent `3`, it is
  enough to prove that this equation has no  solutions such that `c ‚â† 0`, `¬¨ Œª ‚à£ a`, `¬¨ Œª ‚à£ b`,
  `Œª ‚à£ c` and `IsCoprime a b`. We call such a tuple a `Solution'`. A `Solution` is the same as a
  `Solution'` with the additional assumption that `Œª ^ 2 ‚à£ a + b`. We then prove that, given
  `S' : Solution'`, there is `S : Solution` such that the multiplicity of `Œª = Œ∂‚ÇÉ - 1` in `c` is
  the same in `S'` and `S`. In particular it is enough to prove that no `Solution` exists.
  The key point is a descent argument on the multiplicity of `Œª` in `c`: starting with
  `S : Solution` we can find `S‚ÇÅ : Solution` with multiplicity strictly smaller and this finishes
  the proof. To construct `S‚ÇÅ` we go through a `Solution'` and then back to a `Solution`. More
  importantly, we cannot control the unit `u`, and this is the reason why we need to consider
  the generalized equation `a ^ 3 + b ^ 3 = u * c ^ 3`.

-/

section case1

open ZMod

private lemma cube_of_castHom_ne_zero {n : ZMod 9} :
    castHom (show 3 ‚à£ 9 by norm_num) (ZMod 3) n ‚â† 0 ‚Üí n ^ 3 = 1 ‚à® n ^ 3 = 8 := by
  revert n; decide

private lemma cube_of_not_dvd {n : ‚Ñ§} (h : ¬¨ 3 ‚à£ n) :
    (n : ZMod 9) ^ 3 = 1 ‚à® (n : ZMod 9) ^ 3 = 8 := by
  apply cube_of_castHom_ne_zero
  rwa [map_intCast, Ne, ZMod.intCast_zmod_eq_zero_iff_dvd]

/-- If `a b c : ‚Ñ§` are such that `¬¨ 3 ‚à£ a * b * c`, then `a ^ 3 + b ^ 3 ‚â† c ^ 3`. -/
theorem fermatLastTheoremThree_case_1 {a b c : ‚Ñ§} (hdvd : ¬¨ 3 ‚à£ a * b * c) :
    a ^ 3 + b ^ 3 ‚â† c ^ 3 := by
  simp_rw [Int.prime_three.dvd_mul, not_or] at hdvd
  apply mt (congrArg (Int.cast : ‚Ñ§ ‚Üí ZMod 9))
  simp_rw [Int.cast_add, Int.cast_pow]
  rcases cube_of_not_dvd hdvd.1.1 with ha | ha <;>
  rcases cube_of_not_dvd hdvd.1.2 with hb | hb <;>
  rcases cube_of_not_dvd hdvd.2 with hc | hc <;>
  rw [ha, hb, hc] <;> decide

end case1

section case2

private lemma three_dvd_b_of_dvd_a_of_gcd_eq_one_of_case2 {a b c : ‚Ñ§} (ha : a ‚â† 0)
    (Hgcd: Finset.gcd {a, b, c} id = 1) (h3a : 3 ‚à£ a) (HF : a ^ 3 + b ^ 3 + c ^ 3 = 0)
    (H : ‚àÄ a b c : ‚Ñ§, c ‚â† 0 ‚Üí ¬¨ 3 ‚à£ a ‚Üí ¬¨ 3 ‚à£ b  ‚Üí 3 ‚à£ c ‚Üí IsCoprime a b ‚Üí a ^ 3 + b ^ 3 ‚â† c ^ 3) :
    3 ‚à£ b := by
  have hbc : IsCoprime (-b) (-c) := by
    refine IsCoprime.neg_neg ?_
    rw [add_comm (a ^ 3), add_assoc, add_comm (a ^ 3), ‚Üê add_assoc] at HF
    refine isCoprime_of_gcd_eq_one_of_FLT ?_ HF
    convert Hgcd using 2
    rw [Finset.pair_comm, Finset.Insert.comm]
  by_contra! h3b
  by_cases h3c : 3 ‚à£ c
  ¬∑ apply h3b
    rw [add_assoc, add_comm (b ^ 3), ‚Üê add_assoc] at HF
    exact dvd_c_of_prime_of_dvd_a_of_dvd_b_of_FLT Int.prime_three h3a h3c HF
  ¬∑ refine H (-b) (-c) a ha (by simp [h3b]) (by simp [h3c]) h3a hbc ?_
    rw [add_eq_zero_iff_eq_neg, ‚Üê (show Odd 3 by decide).neg_pow] at HF
    rw [‚Üê HF]
    ring

open Finset in
private lemma fermatLastTheoremThree_of_dvd_a_of_gcd_eq_one_of_case2 {a b c : ‚Ñ§} (ha : a ‚â† 0)
    (h3a : 3 ‚à£ a) (Hgcd: Finset.gcd {a, b, c} id = 1)
    (H : ‚àÄ a b c : ‚Ñ§, c ‚â† 0 ‚Üí ¬¨ 3 ‚à£ a ‚Üí ¬¨ 3 ‚à£ b  ‚Üí 3 ‚à£ c ‚Üí IsCoprime a b ‚Üí a ^ 3 + b ^ 3 ‚â† c ^ 3) :
    a ^ 3 + b ^ 3 + c ^ 3 ‚â† 0 := by
  intro HF
  apply (show ¬¨(3 ‚à£ (1 : ‚Ñ§)) by decide)
  rw [‚Üê Hgcd]
  refine dvd_gcd (fun x hx ‚Ü¶ ?_)
  simp only [mem_insert, mem_singleton] at hx
  have h3b : 3 ‚à£ b := by
    refine three_dvd_b_of_dvd_a_of_gcd_eq_one_of_case2 ha ?_ h3a HF H
    simp only [‚Üê Hgcd, gcd_insert, gcd_singleton, id_eq, ‚Üê Int.abs_eq_normalize, abs_neg]
  rcases hx with (hx | hx | hx)
  ¬∑ exact hx ‚ñ∏ h3a
  ¬∑ exact hx ‚ñ∏ h3b
  ¬∑ simpa [hx] using dvd_c_of_prime_of_dvd_a_of_dvd_b_of_FLT Int.prime_three h3a h3b HF

open Finset Int in
/--
  To prove Fermat's Last Theorem for `n = 3`, it is enough to show that that for all `a`, `b`, `c`
  in `‚Ñ§` such that `c ‚â† 0`, `¬¨ 3 ‚à£ a`, `¬¨ 3 ‚à£ b`, `a` and `b` are coprime and `3 ‚à£ c`, we have
  `a ^ 3 + b ^ 3 ‚â† c ^ 3`.
-/
theorem fermatLastTheoremThree_of_three_dvd_only_c
    (H : ‚àÄ a b c : ‚Ñ§, c ‚â† 0 ‚Üí ¬¨ 3 ‚à£ a ‚Üí ¬¨ 3 ‚à£ b  ‚Üí 3 ‚à£ c ‚Üí IsCoprime a b ‚Üí a ^ 3 + b ^ 3 ‚â† c ^ 3) :
    FermatLastTheoremFor 3 := by
  rw [fermatLastTheoremFor_iff_int]
  refine fermatLastTheoremWith_of_fermatLastTheoremWith_coprime (fun a b c ha hb hc Hgcd hF ‚Ü¶?_)
  by_cases h1 : 3 ‚à£ a * b * c
  swap
  ¬∑ exact fermatLastTheoremThree_case_1 h1 hF
  rw [(prime_three).dvd_mul, (prime_three).dvd_mul] at h1
  rw [‚Üê sub_eq_zero, sub_eq_add_neg, ‚Üê (show Odd 3 by decide).neg_pow] at hF
  rcases h1 with ((h3a | h3b) | h3c)
  ¬∑ refine fermatLastTheoremThree_of_dvd_a_of_gcd_eq_one_of_case2 ha h3a ?_ H hF
    simp only [‚Üê Hgcd, Insert.comm, gcd_insert, gcd_singleton, id_eq, ‚Üê abs_eq_normalize, abs_neg]
  ¬∑ rw [add_comm (a ^ 3)] at hF
    refine fermatLastTheoremThree_of_dvd_a_of_gcd_eq_one_of_case2 hb h3b ?_ H hF
    simp only [‚Üê Hgcd, Insert.comm, gcd_insert, gcd_singleton, id_eq, ‚Üê abs_eq_normalize, abs_neg]
  ¬∑ rw [add_comm _ ((-c) ^ 3), ‚Üê add_assoc] at hF
    refine fermatLastTheoremThree_of_dvd_a_of_gcd_eq_one_of_case2 (neg_ne_zero.2 hc) (by simp [h3c])
      ?_ H hF
    rw [Finset.Insert.comm (-c), Finset.pair_comm (-c) b]
    simp only [‚Üê Hgcd, Insert.comm, gcd_insert, gcd_singleton, id_eq, ‚Üê abs_eq_normalize, abs_neg]

section eisenstein

open NumberField IsCyclotomicExtension.Rat.Three

variable {K : Type*} [Field K] [NumberField K] [IsCyclotomicExtension {3} ‚Ñö K]
variable {Œ∂ : K} (hŒ∂ : IsPrimitiveRoot Œ∂ (3 : ‚Ñï+))

local notation3 "Œ∑" => (IsPrimitiveRoot.isUnit (hŒ∂.toInteger_isPrimitiveRoot) (by decide)).unit
local notation3 "Œª" => hŒ∂.toInteger - 1

/-- `FermatLastTheoremForThreeGen` is the statement that `a ^ 3 + b ^ 3 = u * c ^ 3` has no
nontrivial solutions in `ùìû K` for all `u : (ùìû K)À£` such that `¬¨ Œª ‚à£ a`, `¬¨ Œª ‚à£ b` and `Œª ‚à£ c`.
The reason to consider `FermatLastTheoremForThreeGen` is to make a descent argument working. -/
def FermatLastTheoremForThreeGen : Prop :=
  ‚àÄ a b c : ùìû K, ‚àÄ u : (ùìû K)À£, c ‚â† 0 ‚Üí ¬¨ Œª ‚à£ a ‚Üí ¬¨ Œª ‚à£ b  ‚Üí Œª ‚à£ c ‚Üí IsCoprime a b ‚Üí
    a ^ 3 + b ^ 3 ‚â† u * c ^ 3

/-- To prove `FermatLastTheoremFor 3`, it is enough to prove `FermatLastTheoremForThreeGen`. -/
lemma FermatLastTheoremForThree_of_FermatLastTheoremThreeGen :
    FermatLastTheoremForThreeGen hŒ∂ ‚Üí FermatLastTheoremFor 3 := by
  intro H
  refine fermatLastTheoremThree_of_three_dvd_only_c (fun a b c hc ha hb ‚ü®x, hx‚ü© hcoprime h ‚Ü¶ ?_)
  refine H a b c 1 (by simp [hc]) (fun hdvd ‚Ü¶ ha ?_) (fun hdvd ‚Ü¶ hb ?_) ?_ ?_ ?_
  ¬∑ rwa [‚Üê Ideal.norm_dvd_iff (hŒ∂.prime_norm_toInteger_sub_one_of_prime_ne_two' (by decide)),
      hŒ∂.norm_toInteger_sub_one_of_prime_ne_two' (by decide)] at hdvd
  ¬∑ rwa [‚Üê Ideal.norm_dvd_iff (hŒ∂.prime_norm_toInteger_sub_one_of_prime_ne_two' (by decide)),
      hŒ∂.norm_toInteger_sub_one_of_prime_ne_two' (by decide)] at hdvd
  ¬∑ exact dvd_trans hŒ∂.toInteger_sub_one_dvd_prime' ‚ü®x, by simp [hx]‚ü©
  ¬∑ rw [show a = algebraMap _ (ùìû K) a by simp, show b = algebraMap _ (ùìû K) b by simp]
    exact hcoprime.map _
  ¬∑ simp only [Units.val_one, one_mul]
    exact_mod_cast h

namespace FermatLastTheoremForThreeGen

/-- `Solution'` is a tuple given by a solution to `a ^ 3 + b ^ 3 = u * c ^ 3`,
where `a`, `b`, `c` and `u` are as in `FermatLastTheoremForThreeGen`.
See `Solution` for the actual structure on which we will do the descent. -/
structure Solution' where
  a : ùìû K
  b : ùìû K
  c : ùìû K
  u : (ùìû K)À£
  ha : ¬¨ Œª ‚à£ a
  hb : ¬¨ Œª ‚à£ b
  hc : c ‚â† 0
  coprime : IsCoprime a b
  hcdvd : Œª ‚à£ c
  H : a ^ 3 + b ^ 3 = u * c ^ 3
attribute [nolint docBlame] Solution'.a
attribute [nolint docBlame] Solution'.b
attribute [nolint docBlame] Solution'.c
attribute [nolint docBlame] Solution'.u

/-- `Solution` is the same as `Solution'` with the additional assumption that `Œª ^ 2 ‚à£ a + b`. -/
structure Solution extends Solution' hŒ∂ where
  hab : Œª ^ 2 ‚à£ a + b

variable {hŒ∂} (S : Solution hŒ∂) (S' : Solution' hŒ∂) [DecidableRel fun (a b : ùìû K) ‚Ü¶ a ‚à£ b]

/-- For any `S' : Solution'`, the multiplicity of `Œª` in `S'.c` is finite. -/
lemma Solution'.multiplicity_lambda_c_finite :
    multiplicity.Finite (hŒ∂.toInteger - 1) S'.c :=
  multiplicity.finite_of_not_isUnit hŒ∂.zeta_sub_one_prime'.not_unit S'.hc

/-- Given `S' : Solution'`, `S'.multiplicity` is the multiplicity of `Œª` in `S'.c`, as a natural
number. -/
def Solution'.multiplicity :=
  (_root_.multiplicity (hŒ∂.toInteger - 1) S'.c).get (multiplicity_lambda_c_finite S')

/-- Given `S : Solution`, `S.multiplicity` is the multiplicity of `Œª` in `S.c`, as a natural
number. -/
def Solution.multiplicity := S.toSolution'.multiplicity

/-- We say that `S : Solution` is minimal if for all `S‚ÇÅ : Solution`, the multiplicity of `Œª` in
`S.c` is less or equal than the multiplicity in `S‚ÇÅ.c`. -/
def Solution.isMinimal : Prop := ‚àÄ (S‚ÇÅ : Solution hŒ∂), S.multiplicity ‚â§ S‚ÇÅ.multiplicity

/-- If there is a solution then there is a minimal one. -/
lemma Solution.exists_minimal : ‚àÉ (S‚ÇÅ : Solution hŒ∂), S‚ÇÅ.isMinimal := by
  classical
  let T := {n | ‚àÉ (S' : Solution hŒ∂), S'.multiplicity = n}
  rcases Nat.find_spec (‚ü®S.multiplicity, ‚ü®S, rfl‚ü©‚ü© : T.Nonempty) with ‚ü®S‚ÇÅ, hS‚ÇÅ‚ü©
  exact ‚ü®S‚ÇÅ, fun S'' ‚Ü¶ hS‚ÇÅ ‚ñ∏ Nat.find_min' _ ‚ü®S'', rfl‚ü©‚ü©

/-- Given `S' : Solution'`, then `S'.a` and `S'.b` are both congruent to `1` modulo `Œª ^ 4` or are
both congruent to `-1`.  -/
lemma a_cube_b_cube_congr_one_or_neg_one :
    Œª ^ 4 ‚à£ S'.a ^ 3 - 1 ‚àß Œª ^ 4 ‚à£ S'.b ^ 3 + 1 ‚à® Œª ^ 4 ‚à£ S'.a ^ 3 + 1 ‚àß Œª ^ 4 ‚à£ S'.b ^ 3 - 1 := by
  obtain ‚ü®z, hz‚ü© := S'.hcdvd
  rcases lambda_pow_four_dvd_cube_sub_one_or_add_one_of_lambda_not_dvd hŒ∂ S'.ha with
    (‚ü®x, hx‚ü© | ‚ü®x, hx‚ü©) <;>
  rcases lambda_pow_four_dvd_cube_sub_one_or_add_one_of_lambda_not_dvd hŒ∂ S'.hb with
    (‚ü®y, hy‚ü© | ‚ü®y, hy‚ü©)
  ¬∑ exfalso
    replace hŒ∂ : IsPrimitiveRoot Œ∂ ((3 : ‚Ñï+) ^ 1) := by rwa [pow_one]
    refine hŒ∂.toInteger_sub_one_not_dvd_two (by decide) ‚ü®S'.u * Œª ^ 2 * z ^ 3 - Œª ^ 3 * (x + y), ?_‚ü©
    symm
    calc _ = S'.u * (Œª * z) ^ 3 - Œª ^ 4 * x - Œª ^ 4 * y := by ring
    _ = (S'.a ^ 3 + S'.b ^ 3) - (S'.a ^ 3 - 1) - (S'.b ^ 3 - 1) := by rw [‚Üê hx, ‚Üê hy, ‚Üê hz, ‚Üê S'.H]
    _ = 2 := by ring
  ¬∑ left
    exact ‚ü®‚ü®x, hx‚ü©, ‚ü®y, hy‚ü©‚ü©
  ¬∑ right
    exact ‚ü®‚ü®x, hx‚ü©, ‚ü®y, hy‚ü©‚ü©
  ¬∑ exfalso
    replace hŒ∂ : IsPrimitiveRoot Œ∂ ((3 : ‚Ñï+) ^ 1) := by rwa [pow_one]
    refine hŒ∂.toInteger_sub_one_not_dvd_two (by decide) ‚ü®Œª ^ 3 * (x + y) - S'.u * Œª ^ 2 * z ^ 3, ?_‚ü©
    symm
    calc _ =  Œª ^ 4 * x + Œª ^ 4 * y - S'.u * (Œª * z) ^ 3 := by ring
    _ = (S'.a ^ 3 + 1) + (S'.b ^ 3 + 1) - (S'.a ^ 3 + S'.b ^ 3) := by rw [‚Üê hx, ‚Üê hy, ‚Üê hz, ‚Üê S'.H]
    _ = 2 := by ring

/-- Given `S' : Solution'`, we have that `Œª ^ 4` divides `S'.c ^ 3`. -/
lemma lambda_pow_four_dvd_c_cube : Œª ^ 4 ‚à£ S'.c ^ 3 := by
  rcases a_cube_b_cube_congr_one_or_neg_one S' with
    (‚ü®‚ü®x, hx‚ü©, ‚ü®y, hy‚ü©‚ü© | ‚ü®‚ü®x, hx‚ü©, ‚ü®y, hy‚ü©‚ü©) <;>
  ¬∑ refine ‚ü®S'.u‚Åª¬π * (x + y), ?_‚ü©
    symm
    calc _ = S'.u‚Åª¬π * (Œª ^ 4 * x + Œª ^ 4 * y) := by ring
    _ = S'.u‚Åª¬π * (S'.a ^ 3 + S'.b ^ 3) := by rw [‚Üê hx, ‚Üê hy]; ring
    _ = S'.u‚Åª¬π * (S'.u * S'.c ^ 3) := by rw [S'.H]
    _ = S'.c ^ 3 := by simp

/-- Given `S' : Solution'`, we have that `Œª ^ 2` divides `S'.c`. -/
lemma lambda_sq_dvd_c : Œª ^ 2 ‚à£ S'.c := by
  have hm := S'.multiplicity_lambda_c_finite
  suffices 2 ‚â§ (multiplicity ((hŒ∂.toInteger - 1)) S'.c).get hm by
    obtain ‚ü®x, hx‚ü© := multiplicity.pow_multiplicity_dvd hm
    refine ‚ü®Œª ^ ((multiplicity ((hŒ∂.toInteger - 1)) S'.c).get hm - 2) * x, ?_‚ü©
    rw [‚Üê mul_assoc, ‚Üê pow_add]
    convert hx using 3
    simp [this]
  have := lambda_pow_four_dvd_c_cube S'
  have hm1 : (multiplicity (hŒ∂.toInteger - 1) (S'.c ^ 3)).get
    (multiplicity.finite_pow hŒ∂.zeta_sub_one_prime' hm) =
    multiplicity (hŒ∂.toInteger - 1) (S'.c ^ 3) := by simp
  rw [multiplicity.pow_dvd_iff_le_multiplicity, ‚Üê hm1, multiplicity.pow' hŒ∂.zeta_sub_one_prime' hm,
    Nat.cast_ofNat, Nat.ofNat_le_cast] at this
  omega

/-- Given `S' : Solution'`, we have that `2 ‚â§ S'.multiplicity`. -/
lemma Solution'.two_le_multiplicity : 2 ‚â§ S'.multiplicity := by
  simpa [‚Üê PartENat.coe_le_coe, Solution'.multiplicity] using
    multiplicity.le_multiplicity_of_pow_dvd (lambda_sq_dvd_c S')

/-- Given `S : Solution`, we have that `2 ‚â§ S.multiplicity`. -/
lemma Solution.two_le_multiplicity : 2 ‚â§ S.multiplicity :=
  S.toSolution'.two_le_multiplicity

/-- Given `S' : Solution'`, the key factorization of `S'.a ^ 3 + S'.b ^ 3`. -/
lemma a_cube_add_b_cube_eq_mul :
    S'.a ^ 3 + S'.b ^ 3 = (S'.a + S'.b) * (S'.a + Œ∑ * S'.b) * (S'.a + Œ∑ ^ 2 * S'.b) := by
  have := hŒ∂.isRoot_cyclotomic (by decide)
  simp only [PNat.val_ofNat, Polynomial.cyclotomic_three, Polynomial.IsRoot.def,
    Polynomial.eval_add, Polynomial.eval_pow, Polynomial.eval_X, Polynomial.eval_one] at this
  symm
  calc _ = S'.a^3+S'.a^2*S'.b*(Œ∑^2+Œ∑+1)+S'.a*S'.b^2*(Œ∑^2+Œ∑+Œ∑^3)+Œ∑^3*S'.b^3 := by ring
  _ = S'.a^3+S'.a^2*S'.b*(Œ∑^2+Œ∑+1)+S'.a*S'.b^2*(Œ∑^2+Œ∑+1)+S'.b^3 := by
    simp [hŒ∂.toInteger_cube_eq_one]
  _ = S'.a ^ 3 + S'.b ^ 3 := by ext; simp [this]

open PartENat in
/-- Given `S' : Solution'`, we have that `Œª ^ 2` divides one amongst `S'.a + S'.b`,
`S'.a + Œ∑ * S'.b` and `S'.a + Œ∑ ^ 2 * S'.b`. -/
lemma lambda_sq_dvd_or_dvd_or_dvd :
    Œª ^ 2 ‚à£ S'.a + S'.b ‚à® Œª ^ 2 ‚à£ S'.a + Œ∑ * S'.b ‚à® Œª ^ 2 ‚à£ S'.a + Œ∑ ^ 2 * S'.b := by
  by_contra! h
  rcases h with ‚ü®h1, h2, h3‚ü©
  rw [‚Üê multiplicity.multiplicity_lt_iff_not_dvd] at h1 h2 h3
  have h1' : multiplicity.Finite (hŒ∂.toInteger - 1) (S'.a + S'.b) :=
    multiplicity.ne_top_iff_finite.1 (fun ht ‚Ü¶ by simp [ht] at h1)
  have h2' : multiplicity.Finite (hŒ∂.toInteger - 1) (S'.a + Œ∑ * S'.b) := by
    refine multiplicity.ne_top_iff_finite.1 (fun ht ‚Ü¶ ?_)
    rw [coe_eta] at ht
    simp [ht] at h2
  have h3' : multiplicity.Finite (hŒ∂.toInteger - 1) (S'.a + Œ∑ ^ 2 * S'.b) := by
    refine multiplicity.ne_top_iff_finite.1 (fun ht ‚Ü¶ ?_)
    rw [coe_eta] at ht
    simp [ht] at h3
  replace h1' : (multiplicity (hŒ∂.toInteger - 1) (S'.a + S'.b)).get h1' =
    multiplicity (hŒ∂.toInteger - 1) (S'.a + S'.b) := by simp
  replace h2' : (multiplicity (hŒ∂.toInteger - 1) (S'.a + Œ∑ * S'.b)).get h2' =
    multiplicity (hŒ∂.toInteger - 1) (S'.a + Œ∑ * S'.b) := by simp
  replace h3' : (multiplicity (hŒ∂.toInteger - 1) (S'.a + Œ∑ ^ 2 * S'.b)).get h3' =
    multiplicity (hŒ∂.toInteger - 1) (S'.a + Œ∑ ^ 2 * S'.b) := by simp
  rw [‚Üê h1', coe_lt_coe] at h1; rw [‚Üê h2', coe_lt_coe] at h2; rw [‚Üê h3', coe_lt_coe] at h3
  have := (pow_dvd_pow_of_dvd (lambda_sq_dvd_c S') 3).mul_left S'.u
  rw [‚Üê pow_mul, ‚Üê S'.H, a_cube_add_b_cube_eq_mul, multiplicity.pow_dvd_iff_le_multiplicity,
    multiplicity.mul hŒ∂.zeta_sub_one_prime', multiplicity.mul hŒ∂.zeta_sub_one_prime', ‚Üê h1', ‚Üê h2',
    ‚Üê h3', ‚Üê Nat.cast_add, ‚Üê Nat.cast_add, coe_le_coe] at this
  omega

open Units in
/-- Given `S' : Solution'`, we may assume that `Œª ^ 2` divides `S'.a + S'.b ‚à® Œª ^ 2` (see also the
result below). -/
lemma ex_cube_add_cube_eq_and_isCoprime_and_not_dvd_and_dvd :
    ‚àÉ (a' b' : ùìû K), a' ^ 3 + b' ^ 3 = S'.u * S'.c ^ 3 ‚àß IsCoprime a' b' ‚àß ¬¨ Œª ‚à£ a' ‚àß
      ¬¨ Œª ‚à£ b' ‚àß Œª ^ 2 ‚à£ a' + b' := by
  rcases lambda_sq_dvd_or_dvd_or_dvd S' with (h | h | h)
  ¬∑ exact ‚ü®S'.a, S'.b, S'.H, S'.coprime, S'.ha, S'.hb, h‚ü©
  ¬∑ refine ‚ü®S'.a, Œ∑ * S'.b, ?_, ?_, S'.ha, fun ‚ü®x, hx‚ü© ‚Ü¶ S'.hb ‚ü®Œ∑ ^ 2 * x, ?_‚ü©, h‚ü©
    ¬∑ simp [mul_pow, ‚Üê val_pow_eq_pow_val, hŒ∂.toInteger_cube_eq_one, val_one, one_mul, S'.H]
    ¬∑ refine (isCoprime_mul_unit_left_right (Units.isUnit Œ∑) _ _).2 S'.coprime
    ¬∑ rw [mul_comm _ x, ‚Üê mul_assoc, ‚Üê hx, mul_comm _ S'.b, mul_assoc, ‚Üê pow_succ', coe_eta,
        hŒ∂.toInteger_cube_eq_one, mul_one]
  ¬∑ refine ‚ü®S'.a, Œ∑ ^ 2 * S'.b, ?_, ?_, S'.ha, fun ‚ü®x, hx‚ü© ‚Ü¶ S'.hb ‚ü®Œ∑ * x, ?_‚ü©, h‚ü©
    ¬∑ rw [mul_pow, ‚Üê pow_mul, mul_comm 2, pow_mul, coe_eta, hŒ∂.toInteger_cube_eq_one, one_pow,
        one_mul, S'.H]
    ¬∑ exact (isCoprime_mul_unit_left_right ((Units.isUnit Œ∑).pow _) _ _).2 S'.coprime
    ¬∑ rw [mul_comm _ x, ‚Üê mul_assoc, ‚Üê hx, mul_comm _ S'.b, mul_assoc, ‚Üê pow_succ, coe_eta,
        hŒ∂.toInteger_cube_eq_one, mul_one]

/-- Given `S' : Solution'`, then there is `S‚ÇÅ : Solution` such that
`S‚ÇÅ.multiplicity = S'.multiplicity`. -/
lemma exists_Solution_of_Solution' : ‚àÉ (S‚ÇÅ : Solution hŒ∂), S‚ÇÅ.multiplicity = S'.multiplicity := by
  obtain ‚ü®a, b, H, coprime, ha, hb, hab‚ü© := ex_cube_add_cube_eq_and_isCoprime_and_not_dvd_and_dvd S'
  exact ‚ü®
  { a := a
    b := b
    c := S'.c
    u := S'.u
    ha := ha
    hb := hb
    hc := S'.hc
    coprime := coprime
    hcdvd := S'.hcdvd
    H := H
    hab := hab }, rfl‚ü©

namespace Solution

lemma a_add_eta_mul_b : S.a + Œ∑ * S.b = (S.a + S.b) + Œª * S.b := by rw [coe_eta]; ring

/-- Given `(S : Solution)`, we have that `Œª ‚à£ (S.a + Œ∑ * S.b)`. -/
lemma lambda_dvd_a_add_eta_mul_b : Œª ‚à£ (S.a + Œ∑ * S.b) :=
  a_add_eta_mul_b S ‚ñ∏ dvd_add (dvd_trans (dvd_pow_self _ (by decide)) S.hab) ‚ü®S.b, by rw [mul_comm]‚ü©

/-- Given `(S : Solution)`, we have that `Œª ‚à£ (S.a + Œ∑ ^ 2 * S.b)`. -/
lemma lambda_dvd_a_add_eta_sq_mul_b : Œª ‚à£ (S.a + Œ∑ ^ 2 * S.b) := by
  rw [show S.a + Œ∑ ^ 2 * S.b = (S.a + S.b) + Œª ^ 2 * S.b + 2 * Œª * S.b by rw [coe_eta]; ring]
  exact dvd_add (dvd_add (dvd_trans (dvd_pow_self _ (by decide)) S.hab) ‚ü®Œª * S.b, by ring‚ü©)
    ‚ü®2 * S.b, by ring‚ü©

/-- Given `(S : Solution)`, we have that `Œª ^ 2` does not divide `S.a + Œ∑ * S.b`. -/
lemma lambda_sq_not_dvd_a_add_eta_mul_b : ¬¨ Œª ^ 2 ‚à£ (S.a + Œ∑ * S.b) := by
  simp_rw [a_add_eta_mul_b, dvd_add_right S.hab, pow_two, mul_dvd_mul_iff_left
    hŒ∂.zeta_sub_one_prime'.ne_zero, S.hb, not_false_eq_true]

/-- Given `(S : Solution)`, we have that `Œª ^ 2` does not divide `S.a + Œ∑ ^ 2 * S.b`. -/
lemma lambda_sq_not_dvd_a_add_eta_sq_mul_b : ¬¨ Œª ^ 2 ‚à£ (S.a + Œ∑ ^ 2 * S.b) := by
  intro ‚ü®k, hk‚ü©
  rcases S.hab with ‚ü®k', hk'‚ü©
  refine S.hb ‚ü®(k - k') * (-Œ∑), ?_‚ü©
  rw [show S.a + Œ∑ ^ 2 * S.b = S.a + S.b - S.b + Œ∑ ^ 2 * S.b by ring, hk',
    show Œª ^ 2 * k' - S.b + Œ∑ ^ 2 * S.b = Œª * (S.b * (Œ∑ +1) + Œª * k') by rw [coe_eta]; ring,
    pow_two, mul_assoc] at hk
  simp only [mul_eq_mul_left_iff, hŒ∂.zeta_sub_one_prime'.ne_zero, or_false] at hk
  apply_fun (¬∑ * -‚ÜëŒ∑) at hk
  rw [show (S.b * (Œ∑ + 1) + Œª * k') * -Œ∑ = (- S.b) * (Œ∑ ^ 2 + Œ∑ + 1 - 1) - Œ∑ * Œª * k' by ring,
    eta_sq, show -S.b * (-‚ÜëŒ∑ - 1 + ‚ÜëŒ∑ + 1 - 1) = S.b by ring, sub_eq_iff_eq_add] at hk
  rw [hk]
  ring

lemma eta_add_one_mul_neg_eta_eq_one : ((Œ∑ : ùìû K) + 1) * (-Œ∑) = 1 :=
  calc ((Œ∑ : ùìû K) + 1) * -Œ∑ = -(Œ∑ ^ 2 + Œ∑ + 1) + 1 := by ring
  _ = 1 := by rw [eta_sq]; ring

/-- If `p : ùìû K` is a prime that divides both `S.a + S.b` and `S.a + Œ∑ * S.b`, then `p`
is associated with `Œª`. -/
lemma associated_of_dvd_a_add_b_of_dvd_a_add_eta_mul_b {p : ùìû K} (hp : Prime p)
    (hpab : p ‚à£ S.a + S.b) (hpaŒ∑b : p ‚à£ S.a + Œ∑ * S.b) : Associated p Œª := by
  suffices p_lam : p ‚à£ Œª from hp.associated_of_dvd hŒ∂.zeta_sub_one_prime' p_lam
  by_contra p_lam
  refine hp.not_unit <| IsCoprime.isUnit_of_dvd' S.coprime ?_ ?_
  ¬∑ refine (hp.dvd_or_dvd ?_).resolve_left ‚Äπ_‚Ä∫
    rw [show Œª * S.a = Œ∑ * (S.a + S.b) - (S.a + Œ∑ * S.b) by rw [coe_eta]; ring]
    exact dvd_sub (dvd_mul_of_dvd_right hpab _) hpaŒ∑b
  ¬∑ refine (hp.dvd_or_dvd ?_).resolve_left ‚Äπ_‚Ä∫
    rw [show Œª * S.b = (S.a + Œ∑ * S.b) - (S.a + S.b) by rw [coe_eta]; ring]
    exact dvd_sub hpaŒ∑b hpab

/-- If `p : ùìû K` is a prime that divides both `S.a + S.b` and `S.a + Œ∑ ^ 2 * S.b`, then `p`
is associated with `Œª`. -/
lemma associated_of_dvd_a_add_b_of_dvd_a_add_eta_sq_mul_b {p : ùìû K} (hp : Prime p)
    (hpab : p ‚à£ (S.a + S.b)) (hpaŒ∑sqb : p ‚à£ (S.a + Œ∑ ^ 2 * S.b)) : Associated p Œª := by
  suffices p_lam : p ‚à£ Œª from hp.associated_of_dvd hŒ∂.zeta_sub_one_prime' p_lam
  by_contra p_lam
  refine hp.not_unit <| S.coprime.isUnit_of_dvd' ?_ ?_
  ¬∑ refine (hp.dvd_or_dvd ?_).resolve_left p_lam
    rw [show Œª * S.a = - (1 - Œ∑) * S.a by rw [coe_eta]; ring, ‚Üê hŒ∂.toInteger_cube_eq_one]
    rw [show - (hŒ∂.toInteger ^ 3 - Œ∑) * S.a = Œ∑ * ((S.a + Œ∑ ^ 2 * S.b) - Œ∑ ^ 2 * (S.a + S.b))
      by rw [coe_eta]; ring, (Units.isUnit Œ∑).dvd_mul_left]
    exact hpaŒ∑sqb.sub (dvd_mul_of_dvd_right hpab _)
  ¬∑ refine (hp.dvd_or_dvd ?_).resolve_left p_lam
    rw [show Œª * S.b = - (1 - Œ∑) * S.b by rw [coe_eta]; ring, ‚Üê hŒ∂.toInteger_cube_eq_one]
    rw [show - (hŒ∂.toInteger ^ 3 - Œ∑) * S.b = Œ∑ * ((S.a + S.b) - (S.a + Œ∑ ^ 2 * S.b))
      by rw [coe_eta]; ring, (Units.isUnit Œ∑).dvd_mul_left]
    exact hpab.sub hpaŒ∑sqb

/-- If `p : ùìû K` is a prime that divides both `S.a + Œ∑ * S.b` and `S.a + Œ∑ ^ 2 * S.b`, then `p`
is associated with `Œª`. -/
lemma associated_of_dvd_a_add_eta_mul_b_of_dvd_a_add_eta_sq_mul_b {p : ùìû K} (hp : Prime p)
    (hpaŒ∑b : p ‚à£ S.a + Œ∑ * S.b) (hpaŒ∑sqb : p ‚à£ S.a + Œ∑ ^ 2 * S.b) : Associated p Œª := by
  suffices p_lam : p ‚à£ Œª from hp.associated_of_dvd hŒ∂.zeta_sub_one_prime' p_lam
  by_contra p_lam
  refine hp.not_unit <| S.coprime.isUnit_of_dvd' ?_ ?_
  ¬∑ refine (hp.dvd_or_dvd ?_).resolve_left p_lam
    rw [show Œª * S.a = Œ∑ * (S.a + Œ∑ * S.b) - (S.a + Œ∑ ^ 2 * S.b) by rw [coe_eta]; ring]
    exact dvd_mul_of_dvd_right hpaŒ∑b _ |>.sub hpaŒ∑sqb
  ¬∑ refine (hp.dvd_or_dvd ?_).resolve_left p_lam
    rw [‚Üê (Units.isUnit Œ∑).dvd_mul_left, show Œ∑ * (Œª * S.b) = (S.a + Œ∑ ^ 2 * S.b) - (S.a + Œ∑ * S.b)
      by rw [coe_eta]; ring]
    exact hpaŒ∑sqb.sub hpaŒ∑b

/-- Given `S : Solution`, we let `S.y` be any element such that `S.a + Œ∑ * S.b = Œª * S.y` -/
private noncomputable def y := (lambda_dvd_a_add_eta_mul_b S).choose

private lemma y_spec : S.a + Œ∑ * S.b = Œª * S.y :=
  (lambda_dvd_a_add_eta_mul_b S).choose_spec

/-- Given `S : Solution`, we let `S.z` be any element such that `S.a + Œ∑ ^ 2 * S.b = Œª * S.z` -/
private noncomputable def z := (lambda_dvd_a_add_eta_sq_mul_b S).choose

private lemma z_spec : S.a + Œ∑ ^ 2 * S.b = Œª * S.z :=
  (lambda_dvd_a_add_eta_sq_mul_b S).choose_spec

private lemma lambda_not_dvd_y : ¬¨ Œª ‚à£ S.y := fun h ‚Ü¶ by
  replace h := mul_dvd_mul_left ((Œ∑ : ùìû K) - 1) h
  rw [coe_eta, ‚Üê y_spec, ‚Üê pow_two] at h
  exact lambda_sq_not_dvd_a_add_eta_mul_b _ h

private lemma lambda_not_dvd_z : ¬¨ Œª ‚à£ S.z := fun h ‚Ü¶ by
  replace h := mul_dvd_mul_left ((Œ∑ : ùìû K) - 1) h
  rw [coe_eta, ‚Üê z_spec, ‚Üê pow_two] at h
  exact lambda_sq_not_dvd_a_add_eta_sq_mul_b _ h

/-- We have that `Œª ^ (3*S.multiplicity-2)` divides `S.a + S.b`. -/
lemma lambda_pow_dvd_a_add_b : Œª ^ (3 * S.multiplicity - 2) ‚à£ S.a + S.b := by
  have h : Œª ^ S.multiplicity ‚à£ S.c := multiplicity.pow_multiplicity_dvd _
  replace h : (Œª ^ multiplicity S) ^ 3 ‚à£ S.u * S.c ^ 3 := by simp [h]
  rw [‚Üê S.H, a_cube_add_b_cube_eq_mul, ‚Üê pow_mul, mul_comm, y_spec, z_spec] at h
  apply hŒ∂.zeta_sub_one_prime'.pow_dvd_of_dvd_mul_left _ S.lambda_not_dvd_z
  apply hŒ∂.zeta_sub_one_prime'.pow_dvd_of_dvd_mul_left _ S.lambda_not_dvd_y
  have := S.two_le_multiplicity
  rw [show 3 * multiplicity S = 3 * multiplicity S - 2 + 1 + 1 by omega, pow_succ, pow_succ,
    show (S.a + S.b) * (Œª * y S) * (Œª * z S) = (S.a + S.b) * y S * z S * Œª * Œª by ring] at h
  simp only [mul_dvd_mul_iff_right hŒ∂.zeta_sub_one_prime'.ne_zero] at h
  rwa [show (S.a + S.b) * y S * z S = y S * (z S * (S.a + S.b)) by ring] at h

/-- Given `S : Solution`, we let `S.x` be any element such that
`S.a + S.b = Œª ^ (3*S.multiplicity-2) * S.x` -/
private noncomputable def x := (lambda_pow_dvd_a_add_b S).choose

private lemma x_spec : S.a + S.b = Œª ^ (3 * S.multiplicity - 2) * S.x :=
  (lambda_pow_dvd_a_add_b S).choose_spec

/-- Given `S : Solution`, we let `S.w` be any element such that `S.c = Œª ^ S.multiplicity * S.w` -/
private noncomputable def w :=
  (multiplicity.pow_multiplicity_dvd S.toSolution'.multiplicity_lambda_c_finite).choose

private lemma w_spec : S.c = Œª ^ S.multiplicity * S.w :=
  (multiplicity.pow_multiplicity_dvd S.toSolution'.multiplicity_lambda_c_finite).choose_spec

private lemma lambda_not_dvd_w : ¬¨ Œª ‚à£ S.w := by
  intro h
  replace h := mul_dvd_mul_left (Œª ^ S.multiplicity) h
  rw [‚Üê w_spec] at h
  have hh : _ := multiplicity.is_greatest' S.toSolution'.multiplicity_lambda_c_finite
    (lt_add_one S.multiplicity)
  rw [pow_succ', mul_comm] at hh
  exact hh h

private lemma lambda_not_dvd_x : ¬¨ Œª ‚à£ S.x := fun h ‚Ü¶ by
  replace h := mul_dvd_mul_left (Œª ^ (3 * S.multiplicity - 2)) h
  rw [mul_comm, ‚Üê x_spec] at h
  replace h :=
    mul_dvd_mul (mul_dvd_mul h S.lambda_dvd_a_add_eta_mul_b) S.lambda_dvd_a_add_eta_sq_mul_b
  simp only [‚Üê a_cube_add_b_cube_eq_mul, S.H, w_spec, Units.isUnit, IsUnit.dvd_mul_left] at h
  rw [‚Üê pow_succ', mul_comm, ‚Üê mul_assoc, ‚Üê pow_succ'] at h
  have := S.two_le_multiplicity
  rw [show 3 * multiplicity S - 2 + 1 + 1 = 3 * multiplicity S by omega, mul_pow, ‚Üê pow_mul,
    mul_comm _ 3, mul_dvd_mul_iff_left _] at h
  ¬∑ exact lambda_not_dvd_w _ <| hŒ∂.zeta_sub_one_prime'.dvd_of_dvd_pow h
  ¬∑ simp [hŒ∂.zeta_sub_one_prime'.ne_zero]

attribute [local instance] IsCyclotomicExtension.Rat.three_pid

private lemma isCoprime_x_y : IsCoprime S.x S.y := by
  refine isCoprime_of_prime_dvd (not_and.2 (fun _ hy ‚Ü¶ lambda_not_dvd_y S (by simp [hy]))) ?_
  intro p hp p_dvd_x p_dvd_y
  refine lambda_not_dvd_x S ?_
  rw [‚Üê Associated.dvd_iff_dvd_left <| associated_of_dvd_a_add_b_of_dvd_a_add_eta_mul_b S hp ?_ ?_]
  ¬∑ exact p_dvd_x
  ¬∑ rw [x_spec]
    exact dvd_mul_of_dvd_right p_dvd_x (Œª ^ (3 * S.multiplicity - 2))
  ¬∑ convert dvd_mul_of_dvd_right p_dvd_y (Œ∑ -1) using 1
    rw [y_spec, coe_eta]

private lemma isCoprime_x_z : IsCoprime S.x S.z := by
  refine isCoprime_of_prime_dvd (not_and.2 (fun _ hz ‚Ü¶ lambda_not_dvd_z S (by simp [hz]))) ?_
  intro p hp p_dvd_x p_dvd_z
  refine lambda_not_dvd_x S ?_
  rw [‚Üê Associated.dvd_iff_dvd_left <|
    associated_of_dvd_a_add_b_of_dvd_a_add_eta_sq_mul_b S hp ?_ ?_]
  ¬∑ exact p_dvd_x
  ¬∑ rw [x_spec]
    exact dvd_mul_of_dvd_right p_dvd_x (Œª ^ (3 * S.multiplicity - 2))
  ¬∑ convert dvd_mul_of_dvd_right p_dvd_z (Œ∑ - 1) using 1
    rw [z_spec, coe_eta]

private lemma isCoprime_y_z : IsCoprime S.y S.z := by
  refine isCoprime_of_prime_dvd (not_and.2 (fun _ hz ‚Ü¶ lambda_not_dvd_z S (by simp [hz]))) ?_
  intro p hp p_dvd_y p_dvd_z
  refine lambda_not_dvd_y S ?_
  rw [‚Üê Associated.dvd_iff_dvd_left <|
    associated_of_dvd_a_add_eta_mul_b_of_dvd_a_add_eta_sq_mul_b S hp ?_ ?_]
  ¬∑ exact p_dvd_y
  ¬∑ rw [y_spec]
    exact dvd_mul_of_dvd_right p_dvd_y (Œ∑ - 1)
  ¬∑ convert dvd_mul_of_dvd_right p_dvd_z (Œ∑ - 1) using 1
    rw [z_spec, coe_eta]

private lemma x_mul_y_mul_z_eq_u_mul_w_cube : S.x * S.y * S.z = S.u * S.w ^ 3 := by
  suffices hh : Œª ^ (3 * S.multiplicity - 2) * S.x * Œª * S.y * Œª * S.z =
      S.u * Œª ^ (3 * S.multiplicity) * S.w ^ 3 by
    rw [show Œª ^ (3 * multiplicity S - 2) * x S * Œª * y S * Œª * z S =
      Œª ^ (3 * multiplicity S - 2) * Œª * Œª * x S * y S * z S by ring] at hh
    have := S.two_le_multiplicity
    rw [mul_comm _ (Œª ^ (3 * multiplicity S)), ‚Üê pow_succ, ‚Üê pow_succ,
      show 3 * multiplicity S - 2 + 1 + 1 = 3 * multiplicity S by omega, mul_assoc, mul_assoc,
      mul_assoc] at hh
    simp only [mul_eq_mul_left_iff, pow_eq_zero_iff', hŒ∂.zeta_sub_one_prime'.ne_zero, ne_eq,
      mul_eq_zero, OfNat.ofNat_ne_zero, false_or, false_and, or_false] at hh
    convert hh using 1
    ring
  simp only [‚Üê x_spec, mul_assoc, ‚Üê y_spec, ‚Üê z_spec]
  simp only [mul_comm 3, pow_mul, ‚Üê mul_pow, ‚Üê w_spec]
  rw [‚Üê S.H, a_cube_add_b_cube_eq_mul]
  ring

private lemma x_eq_unit_mul_cube : ‚àÉ (u‚ÇÅ : (ùìû K)À£) (X : ùìû K), S.x = u‚ÇÅ * X ^ 3 := by
  have h1 : S.x * (S.y * S.z * S.u‚Åª¬π) = S.w ^ 3 := by
    simp [‚Üê mul_assoc, x_mul_y_mul_z_eq_u_mul_w_cube, mul_comm _ (S.w ^ 3)]
  have h2 : IsCoprime S.x (S.y * S.z * S.u‚Åª¬π) :=
    (isCoprime_mul_unit_right_right (Units.isUnit _) S.x _).2 <|
      IsCoprime.mul_right S.isCoprime_x_y S.isCoprime_x_z
  rcases exists_associated_pow_of_mul_eq_pow' h2 h1 with ‚ü®X, ‚ü®u‚ÇÅ, hX‚ü©‚ü©
  exact ‚ü®u‚ÇÅ, X, by simp [‚Üê hX, mul_comm]‚ü©

private lemma y_eq_unit_mul_cube : ‚àÉ (u‚ÇÇ : (ùìû K)À£) (Y : ùìû K), S.y = u‚ÇÇ * Y ^ 3 := by
  have h1 : S.y * (S.x * S.z * S.u‚Åª¬π) = S.w ^ 3 := by
    rw [‚Üê mul_assoc, ‚Üê mul_assoc S.y, mul_comm S.y, x_mul_y_mul_z_eq_u_mul_w_cube]
    simp only [mul_comm _ (S.w ^ 3), mul_assoc, mul_right_inv, Units.mul_inv, mul_one]
  have h2 : IsCoprime S.y (S.x * S.z * S.u‚Åª¬π) :=
    (isCoprime_mul_unit_right_right (Units.isUnit _) S.y _).2 <|
      IsCoprime.mul_right S.isCoprime_x_y.symm S.isCoprime_y_z
  rcases exists_associated_pow_of_mul_eq_pow' h2 h1 with ‚ü®Y, ‚ü®u‚ÇÇ, hY‚ü©‚ü©
  exact ‚ü®u‚ÇÇ, Y, by simp [‚Üê hY, mul_comm]‚ü©

private lemma z_eq_unit_mul_cube : ‚àÉ (u‚ÇÉ : (ùìû K)À£) (Z : ùìû K), S.z = u‚ÇÉ * Z ^ 3 := by
  have h1 : S.z * (S.x * S.y * S.u‚Åª¬π) = S.w ^ 3 := by
    rw [‚Üê mul_assoc, ‚Üê mul_assoc S.z, mul_comm S.z, mul_assoc S.x, mul_comm S.z, ‚Üê mul_assoc,
      x_mul_y_mul_z_eq_u_mul_w_cube]
    simp only [mul_comm _ (S.w ^ 3), mul_assoc, mul_right_inv, Units.mul_inv, mul_one]
  have h2 : IsCoprime S.z (S.x * S.y * S.u‚Åª¬π) :=
    (isCoprime_mul_unit_right_right (Units.isUnit _) S.z _).2 <|
      IsCoprime.mul_right S.isCoprime_x_z.symm S.isCoprime_y_z.symm
  rcases exists_associated_pow_of_mul_eq_pow' h2 h1 with ‚ü®Z, ‚ü®u‚ÇÉ, hZ‚ü©‚ü©
  exact ‚ü®u‚ÇÉ, Z, by simp [‚Üê hZ, mul_comm]‚ü©

/-- Given `S : Solution`, we let `S.u‚ÇÅ` and `S.X` be any elements such that
`S.x = S.u‚ÇÅ * S.X ^ 3` -/
private noncomputable def u‚ÇÅ := (x_eq_unit_mul_cube S).choose

/-- Given `S : Solution`, we let `S.u‚ÇÅ` and `S.X` be any elements such that
`S.x = S.u‚ÇÅ * S.X ^ 3` -/
private noncomputable def X := (x_eq_unit_mul_cube S).choose_spec.choose

private lemma u‚ÇÅ_X_spec : S.x = S.u‚ÇÅ * S.X ^ 3 := by
  exact (x_eq_unit_mul_cube S).choose_spec.choose_spec

/-- Given `S : Solution`, we let `S.u‚ÇÇ` and `S.Y` be any elements such that
`S.y = S.u‚ÇÇ * S.Y ^ 3` -/
private noncomputable def u‚ÇÇ := (y_eq_unit_mul_cube S).choose

/-- Given `S : Solution`, we let `S.u‚ÇÇ` and `S.Y` be any elements such that
`S.y = S.u‚ÇÇ * S.Y ^ 3` -/
private noncomputable def Y := (y_eq_unit_mul_cube S).choose_spec.choose

private lemma u‚ÇÇ_Y_spec : S.y = S.u‚ÇÇ * S.Y ^ 3 := by
  exact (y_eq_unit_mul_cube S).choose_spec.choose_spec

/-- Given `S : Solution`, we let `S.u‚ÇÉ` and `S.Z` be any elements such that
`S.z = S.u‚ÇÉ * S.Z ^ 3` -/
private noncomputable def u‚ÇÉ := (z_eq_unit_mul_cube S).choose

/-- Given `S : Solution`, we let `S.u‚ÇÉ` and `S.Z` be any elements such that
`S.z = S.u‚ÇÉ * S.Z ^ 3` -/
private noncomputable def Z := (z_eq_unit_mul_cube S).choose_spec.choose

private lemma u‚ÇÉ_Z_spec : S.z = S.u‚ÇÉ * S.Z ^ 3 := by
  exact (z_eq_unit_mul_cube S).choose_spec.choose_spec

end Solution

end FermatLastTheoremForThreeGen

end eisenstein

end case2
