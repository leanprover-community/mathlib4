/-
Copyright (c) 2025 Michail Karatarakis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michail Karatarakis
-/
module

public import Mathlib.Analysis.Analytic.Order
public import Mathlib.Analysis.Complex.CauchyIntegral
public import Mathlib.Analysis.Normed.Module.Connected
public import Mathlib.Tactic

/-!
Some auxiliary lemmata covering the analytic part of the proof of the Gelfond‚ÄìSchneider theorem.
-/

@[expose] public section

variable {ùïú E : Type*} [NontriviallyNormedField ùïú] [NormedAddCommGroup E] [NormedSpace ùïú E]

variable {f g : ùïú ‚Üí E} {n : ‚Ñï} {z‚ÇÄ : ùïú}

open Set AnalyticAt AnalyticOnNhd


lemma eq_zero_on_iff_forall_analyticOrderAt_eq_top {s : Set ùïú} (f : ùïú ‚Üí E) (hs : IsOpen s)
  (_ : AnalyticOn ùïú f s) :
  (‚àÄ z ‚àà s, f z = 0) ‚Üî ‚àÄ z ‚àà s, analyticOrderAt f z = ‚ä§ := by
  constructor
  ¬∑ intro hzero z hz
    have hEv : (fun w => f w) =·∂†[nhds z] (fun _ => (0)) := by
      have : ‚àÄ·∂† w in nhds z, w ‚àà s := hs.mem_nhds hz
      filter_upwards [this] with w hw
      simp [hzero w hw]
    exact (analyticOrderAt_eq_top).2 hEv
  ¬∑ intro htop z hz
    have hEv : (fun w => f w) =·∂†[nhds z] (fun _ => (0)) :=
      (analyticOrderAt_eq_top).1 (htop z hz)
    simpa using hEv.eq_of_nhds

lemma zero_iff_order_inf [PreconnectedSpace ùïú] (f : ùïú ‚Üí E) (z : ùïú) (hf : ‚àÄ z, AnalyticAt ùïú f z) :
  (‚àÄ z, f z = 0) ‚Üî analyticOrderAt f z = ‚ä§ := by
  constructor
  ¬∑ intro H
    refine (analyticOrderAt_eq_top).2 <| (frequently_eq_iff_eventually_eq (hf z) analyticAt_const).1
        (Filter.Frequently.of_forall H)
  ¬∑ intros hr
    rw [@analyticOrderAt_eq_top ùïú _ _ _ _ f z,
        ‚Üê frequently_eq_iff_eventually_eq (hf z) (analyticAt_const)] at hr
    intros z
    exact (eqOn_zero_of_preconnected_of_frequently_eq_zero (fun x hx => by aesop)
      (isPreconnected_univ) trivial hr) trivial

lemma analyticOrderAt_deriv_eq_top_iff_of_eq_zero : ‚àÄ z‚ÇÄ (f : ‚ÑÇ ‚Üí ‚ÑÇ)
     (_ : AnalyticAt ‚ÑÇ f z‚ÇÄ), f z‚ÇÄ = 0 ‚Üí
    ((analyticOrderAt (deriv f) z‚ÇÄ) = ‚ä§ ‚Üî analyticOrderAt f z‚ÇÄ = ‚ä§) := by
  intros z‚ÇÄ f hf hzero
  simp_rw [analyticOrderAt_eq_top,Metric.eventually_nhds_iff_ball]
  constructor
  ¬∑ intros H
    obtain ‚ü®r‚ÇÅ, ‚ü®hr‚ÇÅ0, hB‚ü©‚ü© := exists_ball_analyticOnNhd hf
    obtain ‚ü®r‚ÇÇ, hr‚ÇÇ, hball‚ü© := H
    let r := min r‚ÇÅ r‚ÇÇ
    use r
    have hf : DifferentiableOn ‚ÑÇ f (Metric.ball z‚ÇÄ r) := fun x hx =>
     (hB x (Metric.ball_subset_ball (min_le_left r‚ÇÅ r‚ÇÇ) hx)).differentiableAt.differentiableWithinAt
    have hg : DifferentiableOn ‚ÑÇ (fun _ ‚Ü¶ (0 : ‚ÑÇ)) (Metric.ball z‚ÇÄ r) := differentiableOn_const 0
    have hf' : EqOn (deriv f) (deriv (fun _ ‚Ü¶ (0 : ‚ÑÇ))) (Metric.ball z‚ÇÄ r) := by
      intro x hx
      simpa [deriv_const'] using hball x (Metric.ball_subset_ball (min_le_right r‚ÇÅ r‚ÇÇ) hx)
    have hx : z‚ÇÄ ‚àà Metric.ball z‚ÇÄ r := by
      simpa [Metric.mem_ball, dist_self, r] using (lt_min hr‚ÇÅ0 hr‚ÇÇ)
    have := IsOpen.eqOn_of_deriv_eq (Metric.isOpen_ball)
      (IsConnected.isPreconnected <| Metric.isConnected_ball (by grind)) hf hg hf' hx
    grind
  ¬∑ intros H
    obtain ‚ü®r‚ÇÅ, ‚ü®hr‚ÇÅ0, hB‚ü©‚ü© := exists_ball_analyticOnNhd hf
    obtain ‚ü®r‚ÇÇ, hr‚ÇÇ, hball‚ü© := H
    let r := min r‚ÇÅ r‚ÇÇ
    use r
    constructor
    ¬∑ simp_all only [Metric.mem_ball, dist_self, gt_iff_lt, lt_inf_iff, and_self, r]
    ¬∑ intros x hx
      have hf' : EqOn f 0 (Metric.ball z‚ÇÄ r) :=
        fun x hx ‚Ü¶ hball x (Metric.ball_subset_ball (min_le_right r‚ÇÅ r‚ÇÇ) hx)
      unfold EqOn at hf'
      have hf'' : derivWithin (fun _ => (0 : ‚ÑÇ)) (Metric.ball z‚ÇÄ r) x =
          derivWithin f (Metric.ball z‚ÇÄ r) x := by
        apply Filter.EventuallyEq.derivWithin_eq_of_nhds
        unfold Filter.EventuallyEq
        rw [Filter.eventually_iff_exists_mem]
        use Metric.ball z‚ÇÄ r
        constructor
        ¬∑ refine IsOpen.mem_nhds Metric.isOpen_ball hx
        ¬∑ exact fun z a ‚Ü¶ Eq.symm
            (Complex.ext (congrArg Complex.re (hf' a)) (congrArg Complex.im (hf' a)))
      rw [‚Üê derivWithin_of_mem_nhds]
      ¬∑ rw [‚Üê hf'']; simp only [derivWithin_fun_const, Pi.zero_apply, r]
      ¬∑ rw [IsOpen.mem_nhds_iff]
        ¬∑ exact hx
        ¬∑ aesop

lemma analyticOrderAt_eq_succ_iff_deriv_order_eq_pred (f : ‚ÑÇ ‚Üí ‚ÑÇ) z‚ÇÄ (hf : AnalyticAt ‚ÑÇ f z‚ÇÄ)
  (n : ‚Ñï) : f z‚ÇÄ = 0 ‚Üí analyticOrderAt (deriv f) z‚ÇÄ = (n - 1 : ‚Ñï) ‚Üí
      n > 0 ‚Üí analyticOrderAt f z‚ÇÄ = n := by
    intros hzero horder hn
    have : ‚àÉ m, analyticOrderAt f z‚ÇÄ = m := by simp
    obtain ‚ü®m, Hn'‚ü© := this
    cases m
    ¬∑ exfalso
      have ht : analyticOrderAt (deriv f) z‚ÇÄ = (‚ä§ : ‚Ñï‚àû) :=
        (analyticOrderAt_deriv_eq_top_iff_of_eq_zero z‚ÇÄ f hf hzero).2 Hn'
      exact (ENat.coe_ne_top (n - 1)) (by grind)
    ¬∑ rename_i n'
      cases n'
      ¬∑ exfalso; exact ((AnalyticAt.analyticOrderAt_eq_zero hf).1 Hn') hzero
      ¬∑ rename_i n''
        have hnn : analyticOrderAt (deriv f) z‚ÇÄ = ((n'' + 1) - 1 : ‚Ñï) :=
          Complex.analyticOrderAt_deriv_of_pos f z‚ÇÄ hf (n'' + 1) Hn' (by omega)
        simp only [horder] at hnn
        have : n = n'' + 1 := by
          norm_cast at hnn
          rw [add_tsub_cancel_right] at hnn
          rw [‚Üê hnn]
          exact (Nat.sub_eq_iff_eq_add hn).mp rfl
        rw [this]
        exact Hn'

lemma iterated_deriv_mul_pow_sub_of_analytic (r : ‚Ñï) {z‚ÇÄ : ‚ÑÇ} {R R‚ÇÅ : ‚ÑÇ ‚Üí ‚ÑÇ}
   (hf1 : ‚àÄ z : ‚ÑÇ, AnalyticAt ‚ÑÇ R‚ÇÅ z) (hR‚ÇÅ : ‚àÄ z, R z = (z - z‚ÇÄ)^r * R‚ÇÅ z) :
  --(hf : ‚àÄ z : ‚ÑÇ, AnalyticAt ‚ÑÇ R z) ‚Üí
  ‚àÄ k ‚â§ r ,
    ‚àÉ R‚ÇÇ : ‚ÑÇ ‚Üí ‚ÑÇ, (‚àÄ z : ‚ÑÇ, AnalyticAt ‚ÑÇ R‚ÇÇ z) ‚àß ‚àÄ z, deriv^[k] R z =
   (z - z‚ÇÄ)^(r-k) * (r.factorial/(r-k).factorial * R‚ÇÅ z + (z-z‚ÇÄ)* R‚ÇÇ z) := by
    --intros hf k hkr
      intros k hkr
      induction k
      ¬∑ use 0
        simp only [Function.iterate_zero, id_eq, tsub_zero,
          Pi.zero_apply, mul_zero, add_zero]
        constructor
        ¬∑ intros z; refine Differentiable.analyticAt (differentiable_zero) z
        ¬∑ intros z
          rw [hR‚ÇÅ z]
          simp only [mul_eq_mul_left_iff, pow_eq_zero_iff', ne_eq]
          left
          rw [div_self]
          ¬∑ simp only [one_mul]
          ¬∑ simp only [ne_eq, Nat.cast_eq_zero]
            exact Nat.factorial_ne_zero r
      ¬∑ rename_i k IH
        simp only [Function.iterate_succ, Function.comp_apply]
        have change_deriv (R : ‚ÑÇ ‚Üí ‚ÑÇ) (z: ‚ÑÇ) :
          deriv^[k] (deriv R) z = deriv (deriv^[k] R) z := by
          have : deriv^[k] (deriv R) z = deriv^[k+1] R z := by
           simp only [Function.iterate_succ, Function.comp_apply]
          have : deriv (deriv^[k] R) z = deriv^[k+1] R z := by
            induction k
            ¬∑ simp only [Function.iterate_zero, id_eq, zero_add, Function.iterate_one]
            ¬∑ rename_i k
              simp only [Function.iterate_succ, Function.comp_apply]
              simp only [Function.iterate_succ, Function.comp_apply] at IH
              rw [‚Üê iteratedDeriv_eq_iterate] at *
              rw [‚Üê iteratedDeriv_succ, this]
              simp only [Function.iterate_succ, Function.comp_apply]
          rw [this, ‚Üê this]
          exact id (Eq.symm this)
        simp only [change_deriv R]
        have : k ‚â§ r := by linarith
        have := IH this; clear IH
        obtain ‚ü®R‚ÇÇ, hR‚ÇÇ, hR1‚ü© := this
        let R2 : ‚ÑÇ ‚Üí ‚ÑÇ := fun z =>
           (‚Üë(r - k) * R‚ÇÇ z +
         (‚Üër.factorial / ‚Üë(r - k).factorial * deriv R‚ÇÅ z + (R‚ÇÇ z + (z - z‚ÇÄ) * deriv R‚ÇÇ z)))
        use R2
        constructor
        ¬∑ intro z; dsimp [R2]; fun_prop
        ¬∑ intros z
          have derivOfderivk : ‚àÄ z,
              deriv
                (fun z =>
                  (z - z‚ÇÄ) ^ (r - k) *
                  (r.factorial / (r - k).factorial * R‚ÇÅ z + (z - z‚ÇÄ) * R‚ÇÇ z))
                z =
                ‚Üë(r - k) * (z - z‚ÇÄ) ^ (r - k - 1) *
                  (‚Üër.factorial / ‚Üë(r - k).factorial * R‚ÇÅ z + (z - z‚ÇÄ) * R‚ÇÇ z) +
                (z - z‚ÇÄ) ^ (r - k) *
                  (‚Üër.factorial / ‚Üë(r - k).factorial * deriv R‚ÇÅ z +
                  (R‚ÇÇ z + (z - z‚ÇÄ) * deriv R‚ÇÇ z)) := by
            intro z
            simp (disch := fun_prop)
            [deriv_fun_mul, deriv_fun_add, deriv_fun_pow, deriv_fun_sub, deriv_id'', deriv_const',
          mul_add, add_mul, mul_assoc, mul_left_comm, mul_comm, add_assoc, add_left_comm, add_comm]
          conv => enter [1,1]; ext z; rw [hR1 z]
          rw [derivOfderivk]; clear derivOfderivk
          rw [mul_add]
          have H2 : (r - k - 1) = (r - (k + 1)) := by grind
          rw [H2];
          simp only [add_assoc]
          have H1 :
           ‚Üë(r - k) * (z - z‚ÇÄ) ^ (r - (k + 1)) * (‚Üër.factorial / ‚Üë(r - k).factorial * R‚ÇÅ z)=
           1*((z - z‚ÇÄ) ^ (r - (k + 1)) * (‚Üër.factorial / ‚Üë(r - k).factorial * R‚ÇÅ z)) +
           ‚Üë(r - k - 1) * ((z - z‚ÇÄ) ^ (r - (k + 1)) * (‚Üër.factorial / ‚Üë(r - k).factorial * R‚ÇÅ z))
            := by rw [‚Üê add_mul]; simp only [mul_assoc];congr;norm_cast; grind
          rw [H1]; clear H1;
          simp only [one_mul, ‚Üê mul_assoc]; nth_rw 5 [mul_comm]
          simp only [‚Üê add_assoc, mul_assoc]; rw [‚Üê mul_add]; simp only [‚Üê mul_assoc]
          nth_rw 6 [mul_comm]; nth_rw 7 [mul_comm]; simp only [‚Üê mul_assoc]
          nth_rw 7 [mul_comm]
          simp only [mul_assoc, ‚Üê mul_add]
          have : (z - z‚ÇÄ) ^ (r - k) = (z - z‚ÇÄ) ^ (r - (k + 1)) * (z - z‚ÇÄ)^1 := by
            rw [‚Üê pow_add]; congr; grind
          rw [this];clear this
          simp only [mul_assoc, ‚Üê mul_add, pow_one, mul_eq_mul_left_iff, pow_eq_zero_iff', ne_eq]
          left
          simp only [‚Üê mul_assoc]
          rw [‚Üê add_mul]
          nth_rw 1 [‚Üê one_mul (a:=(r.factorial / (r - k).factorial : ‚ÑÇ))]
          rw [‚Üê add_mul]
          have : ‚Üë(r - (k + 1) + 1)= ‚Üë(r - k) := by grind
          norm_cast
          rw [add_assoc]; simp only [mul_assoc]; rw [‚Üê mul_add, Nat.cast_add, Nat.cast_one]
          nth_rw 2 [add_comm]
          norm_cast
          rw [H2, this]
          simp only [‚Üê mul_assoc, mul_div]
          have : ((‚Üë(r - k) *r.factorial)/‚Üë(r - k).factorial : ‚ÑÇ) =
             ‚Üër.factorial / ‚Üë(r - (k + 1)).factorial := by
            nth_rw 2 [‚Üê Nat.mul_factorial_pred]
            ¬∑ rw [H2]
              ring_nf
              simp only [Nat.cast_mul, _root_.mul_inv_rev]
              nth_rw 2 [mul_comm]; nth_rw 3 [mul_comm]
              simp only [‚Üê mul_assoc, mul_eq_mul_right_iff, inv_eq_zero, Nat.cast_eq_zero]
              left
              rw [mul_assoc, mul_inv_cancel‚ÇÄ]
              ¬∑ simp only [mul_one]
              ¬∑ simp only [ne_eq, Nat.cast_eq_zero]
                grind
            ¬∑ grind
          rw [this]
          unfold R2
          simp only [add_assoc]

lemma analyticOrderAt_eq_nat_iff_iteratedDeriv_eq_zero :
  ‚àÄ z‚ÇÄ n (f : ‚ÑÇ ‚Üí ‚ÑÇ) (_ : AnalyticAt ‚ÑÇ f z‚ÇÄ) (_ : analyticOrderAt f z‚ÇÄ ‚â† ‚ä§),
    (‚àÄ k < n, deriv^[k] f z‚ÇÄ = 0) ‚àß (deriv^[n] f z‚ÇÄ ‚â† 0) ‚Üî analyticOrderAt f z‚ÇÄ = n := by
  intros z‚ÇÄ n
  induction n
  ¬∑ simp only [ne_eq, not_lt_zero', IsEmpty.forall_iff, implies_true,
      Function.iterate_zero, id_eq, true_and, CharP.cast_eq_zero]
    exact fun f hf ho ‚Ü¶ Iff.symm (AnalyticAt.analyticOrderAt_eq_zero hf)
  ¬∑ rename_i n IH
    intros f hf hfin
    constructor
    ¬∑ intros H
      obtain ‚ü®hz, hnz‚ü©:= H
      have IH' := IH (deriv f) (AnalyticAt.deriv hf) ?_
      ¬∑ suffices analyticOrderAt (deriv f) z‚ÇÄ = (n : ‚Ñï) by
          refine analyticOrderAt_eq_succ_iff_deriv_order_eq_pred f z‚ÇÄ hf
            (n + 1) (hz 0 (by omega)) this (by simp)
        rw[‚Üê IH']
        constructor
        ¬∑ intros k hk; exact hz (k + 1) (by omega)
        ¬∑ exact hnz
      ¬∑ have := Complex.analyticOrderAt_deriv_of_pos f  z‚ÇÄ hf
        specialize hz 0 (by omega)
        obtain ‚ü®r, hr‚ü© := (WithTop.ne_top_iff_exists).mp hfin
        specialize this r hr.symm
        simp only [Function.iterate_zero, id_eq] at hz
        have r0 : r > 0 := by
          suffices analyticOrderAt f z‚ÇÄ > 0 by
            suffices @WithTop.some ‚Ñï r > 0 by exact ENat.coe_lt_coe.mp this
            rw [hr]
            exact this
          exact pos_of_ne_zero (analyticOrderAt_ne_zero.mpr ‚ü®hf, hz‚ü©)
        have Hr : r ‚â† 0 := by omega
        specialize this Hr
        rw [this]
        exact ENat.coe_ne_top (r - 1)
    ¬∑ intros ho
      constructor
      ¬∑ intros k hk
        have : analyticOrderAt (deriv^[k] f) z‚ÇÄ ‚â† 0 := by
          rw [(Complex.analyticOrderAt_iterated_deriv f hf k (n+1)
            ho.symm (by omega) hk.le), @Nat.cast_ne_zero]
          omega
        rw [analyticOrderAt_ne_zero] at this
        exact this.2
      ¬∑ have := Complex.analyticOrderAt_iterated_deriv f hf (n+1) (n+1)
          ho.symm (by omega) (by omega)
        simp only [Function.iterate_succ, Function.comp_apply, tsub_self,
          CharP.cast_eq_zero] at this
        rw [AnalyticAt.analyticOrderAt_eq_zero] at this
        ¬∑ assumption
        ¬∑ exact iterated_deriv hf (n + 1)

lemma analyticOrderAt_eq_nat_imp_iteratedDeriv_eq_zero
    z‚ÇÄ (n : ‚Ñï) (f : ‚ÑÇ ‚Üí ‚ÑÇ) (hf : AnalyticAt ‚ÑÇ f z‚ÇÄ) :
  analyticOrderAt f z‚ÇÄ = n ‚Üí (‚àÄ k < n, deriv^[k] f z‚ÇÄ = 0) ‚àß (deriv^[n] f z‚ÇÄ ‚â† 0) := fun h =>
  (analyticOrderAt_eq_nat_iff_iteratedDeriv_eq_zero z‚ÇÄ n f hf (h.symm ‚ñ∏ ENat.coe_ne_top n)).mpr h

lemma le_analyticOrderAt_iff_iteratedDeriv_eq_zero : ‚àÄ (n : ‚Ñï) z‚ÇÄ
  (f : ‚ÑÇ ‚Üí ‚ÑÇ) (_ : AnalyticAt ‚ÑÇ f z‚ÇÄ) (_ : analyticOrderAt f z‚ÇÄ ‚â† ‚ä§),
  (‚àÄ k < n, (deriv^[k] f) z‚ÇÄ = 0) ‚Üí n ‚â§ analyticOrderAt f z‚ÇÄ := by
    intros n z‚ÇÄ f hf ho hkn
    have notTop (m : ‚Ñï‚àû): m ‚â† ‚ä§ ‚Üí ‚àÉ n : ‚Ñï, m = ‚Üën := by
      intro h
      exact Option.ne_none_iff_exists'.mp h
    obtain ‚ü®m, Hm‚ü© := notTop (analyticOrderAt f z‚ÇÄ) ho
    rw [Hm]
    rw [‚Üê analyticOrderAt_eq_nat_iff_iteratedDeriv_eq_zero z‚ÇÄ m f hf ho] at Hm
    rw [ENat.coe_le_coe]
    by_contra h
    push_neg at h
    exact Hm.2 (hkn m h)

lemma hasFPowerSeriesWithinAt_nhds_iff (f : ‚ÑÇ ‚Üí ‚ÑÇ) (p : FormalMultilinearSeries ‚ÑÇ ‚ÑÇ ‚ÑÇ)
    (U : Set ‚ÑÇ) (z : ‚ÑÇ) (hU : U ‚àà nhds z) :
  HasFPowerSeriesWithinAt f p U z ‚Üî HasFPowerSeriesAt f p z := by
    simp only [HasFPowerSeriesWithinAt, HasFPowerSeriesAt]
    constructor
    ¬∑ intros H
      have hzmem : z ‚àà U := by exact mem_of_mem_nhds hU
      rw [Metric.mem_nhds_iff] at hU
      obtain ‚ü®r', hr', hball‚ü©:= hU
      let r'' : ENNReal := Option.some ‚ü®r', by linarith‚ü©
      have hball' : EMetric.ball z r'' ‚äÜ U := by aesop
      obtain ‚ü®renn, hr‚ü©:= H
      use min (renn) r''
      obtain ‚ü®r_le, r_pos, hs‚ü© := hr
      constructor
      ¬∑ aesop
      ¬∑ aesop
      ¬∑ intros y a
        apply hs
        ¬∑ have shift_ball (x y z : ‚ÑÇ) (renn : ENNReal) :
            x ‚àà EMetric.ball y renn ‚Üí z + x ‚àà EMetric.ball (z + y) renn := by
              simp only [EMetric.mem_ball, edist_add_left, imp_self]
          have : z + y ‚àà EMetric.ball (z + 0) (min renn r'') := by
            apply shift_ball
            exact a
          have : z + y ‚àà EMetric.ball z (min renn r'') := by aesop
          have : z + y ‚àà EMetric.ball z r'' := by aesop
          have : z + y ‚àà U := by aesop
          aesop
        ¬∑ aesop
    ¬∑ intros H
      obtain ‚ü®renn,hr‚ü©:= H
      use renn
      exact HasFPowerSeriesOnBall.hasFPowerSeriesWithinOnBall hr

lemma AnalyticOn.analyticAt (f : ‚ÑÇ ‚Üí ‚ÑÇ) (z : ‚ÑÇ) (U : Set ‚ÑÇ) (hU : U ‚àà nhds z) :
  AnalyticOn ‚ÑÇ f U ‚Üí AnalyticAt ‚ÑÇ f z := by
  intros HA
  obtain ‚ü®p, hp‚ü© := (HA z (mem_of_mem_nhds hU))
  use p
  exact (hasFPowerSeriesWithinAt_nhds_iff f p U z hU).mp hp
