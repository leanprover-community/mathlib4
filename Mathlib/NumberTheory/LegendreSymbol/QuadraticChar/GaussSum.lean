/-
Copyright (c) 2022 Michael Stoll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Stoll
-/
import Mathlib.NumberTheory.LegendreSymbol.QuadraticChar.Basic
import Mathlib.NumberTheory.LegendreSymbol.GaussSum

#align_import number_theory.legendre_symbol.quadratic_char.gauss_sum from "leanprover-community/mathlib"@"5b2fe80501ff327b9109fb09b7cc8c325cd0d7d9"

/-!
# Quadratic characters of finite fields

Further facts relying on Gauss sums.

-/


/-!
### Basic properties of the quadratic character

We prove some properties of the quadratic character.
We work with a finite field `F` here.
The interesting case is when the characteristic of `F` is odd.
-/


section SpecialValues

open ZMod MulChar

variable {F : Type*} [Field F] [Fintype F]

/-- The value of the quadratic character at `2` -/
theorem quadraticChar_two [DecidableEq F] (hF : ringChar F ‚â† 2) :
    quadraticChar F 2 = œá‚Çà (Fintype.card F) :=
  IsQuadratic.eq_of_eq_coe (quadraticChar_isQuadratic F) isQuadratic_œá‚Çà hF
    ((quadraticChar_eq_pow_of_char_ne_two' hF 2).trans (FiniteField.two_pow_card hF))
#align quadratic_char_two quadraticChar_two

/-- `2` is a square in `F` iff `#F` is not congruent to `3` or `5` mod `8`. -/
theorem FiniteField.isSquare_two_iff :
    IsSquare (2 : F) ‚Üî Fintype.card F % 8 ‚â† 3 ‚àß Fintype.card F % 8 ‚â† 5 := by
  classical
  by_cases hF : ringChar F = 2
  focus
    have h := FiniteField.even_card_of_char_two hF
    simp only [FiniteField.isSquare_of_char_two hF, true_iff_iff]
  rotate_left
  focus
    have h := FiniteField.odd_card_of_char_ne_two hF
    rw [‚Üê quadraticChar_one_iff_isSquare (Ring.two_ne_zero hF), quadraticChar_two hF,
      œá‚Çà_nat_eq_if_mod_eight]
    simp only [h, Nat.one_ne_zero, if_false, ite_eq_left_iff, Ne.def, (by decide : (-1 : ‚Ñ§) ‚â† 1),
      imp_false, Classical.not_not]
  all_goals
    rw [‚Üê Nat.mod_mod_of_dvd _ (by norm_num : 2 ‚à£ 8)] at h
    have h‚ÇÅ := Nat.mod_lt (Fintype.card F) (by decide : 0 < 8)
    revert h‚ÇÅ h
    generalize Fintype.card F % 8 = n
    intros; interval_cases n <;> simp_all -- Porting note: was `decide!`
#align finite_field.is_square_two_iff FiniteField.isSquare_two_iff

/-- The value of the quadratic character at `-2` -/
theorem quadraticChar_neg_two [DecidableEq F] (hF : ringChar F ‚â† 2) :
    quadraticChar F (-2) = œá‚Çà' (Fintype.card F) := by
  rw [(by norm_num : (-2 : F) = -1 * 2), map_mul, œá‚Çà'_eq_œá‚ÇÑ_mul_œá‚Çà, quadraticChar_neg_one hF,
    quadraticChar_two hF, @cast_nat_cast _ (ZMod 4) _ _ _ (by norm_num : 4 ‚à£ 8)]
#align quadratic_char_neg_two quadraticChar_neg_two

/-- `-2` is a square in `F` iff `#F` is not congruent to `5` or `7` mod `8`. -/
theorem FiniteField.isSquare_neg_two_iff :
    IsSquare (-2 : F) ‚Üî Fintype.card F % 8 ‚â† 5 ‚àß Fintype.card F % 8 ‚â† 7 := by
  classical
  by_cases hF : ringChar F = 2
  focus
    have h := FiniteField.even_card_of_char_two hF
    simp only [FiniteField.isSquare_of_char_two hF, true_iff_iff]
  rotate_left
  focus
    have h := FiniteField.odd_card_of_char_ne_two hF
    rw [‚Üê quadraticChar_one_iff_isSquare (neg_ne_zero.mpr (Ring.two_ne_zero hF)),
      quadraticChar_neg_two hF, œá‚Çà'_nat_eq_if_mod_eight]
    simp only [h, Nat.one_ne_zero, if_false, ite_eq_left_iff, Ne.def, (by decide : (-1 : ‚Ñ§) ‚â† 1),
      imp_false, Classical.not_not]
  all_goals
    rw [‚Üê Nat.mod_mod_of_dvd _ (by norm_num : 2 ‚à£ 8)] at h
    have h‚ÇÅ := Nat.mod_lt (Fintype.card F) (by decide : 0 < 8)
    revert h‚ÇÅ h
    generalize Fintype.card F % 8 = n
    intros; interval_cases n <;> simp_all -- Porting note: was `decide!`
#align finite_field.is_square_neg_two_iff FiniteField.isSquare_neg_two_iff

/-- The relation between the values of the quadratic character of one field `F` at the
cardinality of another field `F'` and of the quadratic character of `F'` at the cardinality
of `F`. -/
theorem quadraticChar_card_card [DecidableEq F] (hF : ringChar F ‚â† 2) {F' : Type*} [Field F']
    [Fintype F'] [DecidableEq F'] (hF' : ringChar F' ‚â† 2) (h : ringChar F' ‚â† ringChar F) :
    quadraticChar F (Fintype.card F') =
    quadraticChar F' (quadraticChar F (-1) * Fintype.card F) := by
  let œá := (quadraticChar F).ringHomComp (algebraMap ‚Ñ§ F')
  -- ‚ä¢ ‚Üë(quadraticChar F) ‚Üë(Fintype.card F') = ‚Üë(quadraticChar F') (‚Üë(‚Üë(quadraticCh ‚Ä¶
  have hœá‚ÇÅ : œá.IsNontrivial := by
    obtain ‚ü®a, ha‚ü© := quadraticChar_exists_neg_one hF
    have hu : IsUnit a := by
      contrapose ha
      exact ne_of_eq_of_ne (map_nonunit (quadraticChar F) ha) (mt zero_eq_neg.mp one_ne_zero)
    use hu.unit
    simp only [IsUnit.unit_spec, ringHomComp_apply, eq_intCast, Ne.def, ha]
    rw [Int.cast_neg, Int.cast_one]
    exact Ring.neg_one_ne_one_of_char_ne_two hF'
  have hœá‚ÇÇ : œá.IsQuadratic := IsQuadratic.comp (quadraticChar_isQuadratic F) _
  -- ‚ä¢ ‚Üë(quadraticChar F) ‚Üë(Fintype.card F') = ‚Üë(quadraticChar F') (‚Üë(‚Üë(quadraticCh ‚Ä¶
  have h := Char.card_pow_card hœá‚ÇÅ hœá‚ÇÇ h hF'
  -- ‚ä¢ ‚Üë(quadraticChar F) ‚Üë(Fintype.card F') = ‚Üë(quadraticChar F') (‚Üë(‚Üë(quadraticCh ‚Ä¶
  rw [‚Üê quadraticChar_eq_pow_of_char_ne_two' hF'] at h
  -- ‚ä¢ ‚Üë(quadraticChar F) ‚Üë(Fintype.card F') = ‚Üë(quadraticChar F') (‚Üë(‚Üë(quadraticCh ‚Ä¶
  exact (IsQuadratic.eq_of_eq_coe (quadraticChar_isQuadratic F')
    (quadraticChar_isQuadratic F) hF' h).symm
#align quadratic_char_card_card quadraticChar_card_card

/-- The value of the quadratic character at an odd prime `p` different from `ringChar F`. -/
theorem quadraticChar_odd_prime [DecidableEq F] (hF : ringChar F ‚â† 2) {p : ‚Ñï} [Fact p.Prime]
    (hp‚ÇÅ : p ‚â† 2) (hp‚ÇÇ : ringChar F ‚â† p) :
    quadraticChar F p = quadraticChar (ZMod p) (œá‚ÇÑ (Fintype.card F) * Fintype.card F) := by
  rw [‚Üê quadraticChar_neg_one hF]
  -- ‚ä¢ ‚Üë(quadraticChar F) ‚Üëp = ‚Üë(quadraticChar (ZMod p)) (‚Üë(‚Üë(quadraticChar F) (-1) ‚Ä¶
  have h := quadraticChar_card_card hF (ne_of_eq_of_ne (ringChar_zmod_n p) hp‚ÇÅ)
    (ne_of_eq_of_ne (ringChar_zmod_n p) hp‚ÇÇ.symm)
  rwa [card p] at h
  -- üéâ no goals
#align quadratic_char_odd_prime quadraticChar_odd_prime

/-- An odd prime `p` is a square in `F` iff the quadratic character of `ZMod p` does not
take the value `-1` on `œá‚ÇÑ#F * #F`. -/
theorem FiniteField.isSquare_odd_prime_iff (hF : ringChar F ‚â† 2) {p : ‚Ñï} [Fact p.Prime]
    (hp : p ‚â† 2) :
    IsSquare (p : F) ‚Üî quadraticChar (ZMod p) (œá‚ÇÑ (Fintype.card F) * Fintype.card F) ‚â† -1 := by
  classical
  by_cases hFp : ringChar F = p
  ¬∑ rw [show (p : F) = 0 by rw [‚Üê hFp]; exact ringChar.Nat.cast_ringChar]
    simp only [isSquare_zero, Ne.def, true_iff_iff, map_mul]
    obtain ‚ü®n, _, hc‚ü© := FiniteField.card F (ringChar F)
    have hchar : ringChar F = ringChar (ZMod p) := by rw [hFp]; exact (ringChar_zmod_n p).symm
    conv => enter [1, 1, 2]; rw [hc, Nat.cast_pow, map_pow, hchar, map_ringChar]
    simp only [zero_pow n.pos, mul_zero, zero_eq_neg, one_ne_zero, not_false_iff]
  ¬∑ rw [‚Üê Iff.not_left (@quadraticChar_neg_one_iff_not_isSquare F _ _ _ _),
      quadraticChar_odd_prime hF hp]
    exact hFp
#align finite_field.is_square_odd_prime_iff FiniteField.isSquare_odd_prime_iff

end SpecialValues
