/-
Copyright (c) 2022 Michael Stoll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Stoll
-/
import Mathlib.NumberTheory.Cyclotomic.PrimitiveRoots
import Mathlib.FieldTheory.Finite.Trace

#align_import number_theory.legendre_symbol.add_character from "leanprover-community/mathlib"@"0723536a0522d24fc2f159a096fb3304bef77472"

/-!
# Additive characters of finite rings and fields

Let `R` be a finite commutative ring. An *additive character* of `R` with values
in another commutative ring `R'` is simply a morphism from the additive group
of `R` into the multiplicative monoid of `R'`.

The additive characters on `R` with values in `R'` form a commutative group.

We use the namespace `AddChar`.

## Main definitions and results

We define `mulShift Ïˆ a`, where `Ïˆ : AddChar R R'` and `a : R`, to be the
character defined by `x â†¦ Ïˆ (a * x)`. An additive character `Ïˆ` is *primitive*
if `mulShift Ïˆ a` is trivial only when `a = 0`.

We show that when `Ïˆ` is primitive, then the map `a â†¦ mulShift Ïˆ a` is injective
(`AddChar.to_mulShift_inj_of_isPrimitive`) and that `Ïˆ` is primitive when `R` is a field
and `Ïˆ` is nontrivial (`AddChar.IsNontrivial.isPrimitive`).

We also show that there are primitive additive characters on `R` (with suitable
target `R'`) when `R` is a field or `R = ZMod n` (`AddChar.primitiveCharFiniteField`
and `AddChar.primitiveZModChar`).

Finally, we show that the sum of all character values is zero when the character
is nontrivial (and the target is a domain); see `AddChar.sum_eq_zero_of_isNontrivial`.

## Tags

additive character
-/


universe u v

/-!
### Definitions related to and results on additive characters
-/


section AddCharDef

-- The domain of our additive characters
variable (R : Type u) [AddMonoid R]

-- The target
variable (R' : Type v) [CommMonoid R']

/-- Define `AddChar R R'` as `(Multiplicative R) â†’* R'`.
The definition works for an additive monoid `R` and a monoid `R'`,
but we will restrict to the case that both are commutative rings below.
We assume right away that `R'` is commutative, so that `AddChar R R'` carries
a structure of commutative monoid.
The trivial additive character (sending everything to `1`) is `(1 : AddChar R R').` -/
def AddChar : Type max u v :=
  Multiplicative R â†’* R'
#align add_char AddChar

end AddCharDef

namespace AddChar

-- Porting note(https://github.com/leanprover-community/mathlib4/issues/5020): added
section DerivedInstances

variable (R : Type u) [AddMonoid R] (R' : Type v) [CommMonoid R']

instance : CommMonoid (AddChar R R') :=
  inferInstanceAs (CommMonoid (Multiplicative R â†’* R'))

instance : Inhabited (AddChar R R') :=
  inferInstanceAs (Inhabited (Multiplicative R â†’* R'))

end DerivedInstances

section CoeToFun

variable {R : Type u} [AddMonoid R] {R' : Type v} [CommMonoid R']

/-- Interpret an additive character as a monoid homomorphism. -/
def toMonoidHom : AddChar R R' â†’ Multiplicative R â†’* R' :=
  id
#align add_char.to_monoid_hom AddChar.toMonoidHom

open Multiplicative

-- Porting note: added.
@[coe]
def toFun (Ïˆ : AddChar R R') (x : R) : R' := Ïˆ.toMonoidHom (ofAdd x)

/-- Define coercion to a function so that it includes the move from `R` to `Multiplicative R`.
After we have proved the API lemmas below, we don't need to worry about writing `ofAdd a`
when we want to apply an additive character. -/
instance hasCoeToFun : CoeFun (AddChar R R') fun _ => R â†’ R' where
  coe := toFun
#align add_char.has_coe_to_fun AddChar.hasCoeToFun

theorem coe_to_fun_apply (Ïˆ : AddChar R R') (a : R) : Ïˆ a = Ïˆ.toMonoidHom (ofAdd a) :=
  rfl
#align add_char.coe_to_fun_apply AddChar.coe_to_fun_apply

-- porting note: added
theorem mul_apply (Ïˆ Ï† : AddChar R R') (a : R) : (Ïˆ * Ï†) a = Ïˆ a * Ï† a :=
  rfl

-- porting note: added
@[simp]
theorem one_apply (a : R) : (1 : AddChar R R') a = 1 := rfl

instance monoidHomClass : MonoidHomClass (AddChar R R') (Multiplicative R) R' :=
  MonoidHom.monoidHomClass
#align add_char.monoid_hom_class AddChar.monoidHomClass

-- Porting note(https://github.com/leanprover-community/mathlib4/issues/5229): added.
@[ext]
theorem ext (f g : AddChar R R') (h : âˆ€ x : R, f x = g x) : f = g :=
  MonoidHom.ext h

/-- An additive character maps `0` to `1`. -/
@[simp]
theorem map_zero_one (Ïˆ : AddChar R R') : Ïˆ 0 = 1 := by rw [coe_to_fun_apply, ofAdd_zero, map_one]
                                                        -- ğŸ‰ no goals
#align add_char.map_zero_one AddChar.map_zero_one

/-- An additive character maps sums to products. -/
@[simp]
theorem map_add_mul (Ïˆ : AddChar R R') (x y : R) : Ïˆ (x + y) = Ïˆ x * Ïˆ y := by
  rw [coe_to_fun_apply, coe_to_fun_apply _ x, coe_to_fun_apply _ y, ofAdd_add, map_mul]
  -- ğŸ‰ no goals
#align add_char.map_add_mul AddChar.map_add_mul

/-- An additive character maps multiples by natural numbers to powers. -/
@[simp]
theorem map_nsmul_pow (Ïˆ : AddChar R R') (n : â„•) (x : R) : Ïˆ (n â€¢ x) = Ïˆ x ^ n := by
  rw [coe_to_fun_apply, coe_to_fun_apply _ x, ofAdd_nsmul, map_pow]
  -- ğŸ‰ no goals
#align add_char.map_nsmul_pow AddChar.map_nsmul_pow

end CoeToFun

section GroupStructure

open Multiplicative

variable {R : Type u} [AddCommGroup R] {R' : Type v} [CommMonoid R']

/-- An additive character on a commutative additive group has an inverse.

Note that this is a different inverse to the one provided by `MonoidHom.inv`,
as it acts on the domain instead of the codomain. -/
instance hasInv : Inv (AddChar R R') :=
  âŸ¨fun Ïˆ => Ïˆ.comp invMonoidHomâŸ©
#align add_char.has_inv AddChar.hasInv

theorem inv_apply (Ïˆ : AddChar R R') (x : R) : Ïˆâ»Â¹ x = Ïˆ (-x) :=
  rfl
#align add_char.inv_apply AddChar.inv_apply

/-- An additive character maps multiples by integers to powers. -/
@[simp]
theorem map_zsmul_zpow {R' : Type v} [CommGroup R'] (Ïˆ : AddChar R R') (n : â„¤) (x : R) :
    Ïˆ (n â€¢ x) = Ïˆ x ^ n := by rw [coe_to_fun_apply, coe_to_fun_apply _ x, ofAdd_zsmul, map_zpow]
                              -- ğŸ‰ no goals
#align add_char.map_zsmul_zpow AddChar.map_zsmul_zpow

/-- The additive characters on a commutative additive group form a commutative group. -/
instance commGroup : CommGroup (AddChar R R') :=
  { MonoidHom.commMonoid with
    inv := Inv.inv
    mul_left_inv := fun Ïˆ => by
      ext x
      -- âŠ¢ â†‘(Ïˆâ»Â¹ * Ïˆ) x = â†‘1 x
      rw [AddChar.mul_apply, AddChar.one_apply, inv_apply, â† map_add_mul, add_left_neg,
        map_zero_one] }
#align add_char.comm_group AddChar.commGroup

end GroupStructure

section Additive

-- The domain and target of our additive characters. Now we restrict to rings on both sides.
variable {R : Type u} [CommRing R] {R' : Type v} [CommRing R']

/-- An additive character is *nontrivial* if it takes a value `â‰  1`. -/
def IsNontrivial (Ïˆ : AddChar R R') : Prop :=
  âˆƒ a : R, Ïˆ a â‰  1
#align add_char.is_nontrivial AddChar.IsNontrivial

/-- An additive character is nontrivial iff it is not the trivial character. -/
theorem isNontrivial_iff_ne_trivial (Ïˆ : AddChar R R') : IsNontrivial Ïˆ â†” Ïˆ â‰  1 := by
  refine' not_forall.symm.trans (Iff.not _)
  -- âŠ¢ (âˆ€ (x : R), â†‘Ïˆ x = 1) â†” Ïˆ = 1
  rw [FunLike.ext_iff]
  -- âŠ¢ (âˆ€ (x : R), â†‘Ïˆ x = 1) â†” âˆ€ (x : Multiplicative R), â†‘Ïˆ x = â†‘1 x
  rfl
  -- ğŸ‰ no goals
#align add_char.is_nontrivial_iff_ne_trivial AddChar.isNontrivial_iff_ne_trivial

/-- Define the multiplicative shift of an additive character.
This satisfies `mulShift Ïˆ a x = Ïˆ (a * x)`. -/
def mulShift (Ïˆ : AddChar R R') (a : R) : AddChar R R' :=
  Ïˆ.comp (AddMonoidHom.toMultiplicative (AddMonoidHom.mulLeft a))
#align add_char.mul_shift AddChar.mulShift

@[simp]
theorem mulShift_apply {Ïˆ : AddChar R R'} {a : R} {x : R} : mulShift Ïˆ a x = Ïˆ (a * x) :=
  rfl
#align add_char.mul_shift_apply AddChar.mulShift_apply

/-- `Ïˆâ»Â¹ = mulShift Ïˆ (-1))`. -/
theorem inv_mulShift (Ïˆ : AddChar R R') : Ïˆâ»Â¹ = mulShift Ïˆ (-1) := by
  ext
  -- âŠ¢ â†‘Ïˆâ»Â¹ xâœ = â†‘(mulShift Ïˆ (-1)) xâœ
  rw [inv_apply, mulShift_apply, neg_mul, one_mul]
  -- ğŸ‰ no goals
#align add_char.inv_mul_shift AddChar.inv_mulShift

/-- If `n` is a natural number, then `mulShift Ïˆ n x = (Ïˆ x) ^ n`. -/
theorem mulShift_spec' (Ïˆ : AddChar R R') (n : â„•) (x : R) : mulShift Ïˆ n x = Ïˆ x ^ n := by
  rw [mulShift_apply, â† nsmul_eq_mul, map_nsmul_pow]
  -- ğŸ‰ no goals
#align add_char.mul_shift_spec' AddChar.mulShift_spec'

/-- If `n` is a natural number, then `Ïˆ ^ n = mulShift Ïˆ n`. -/
theorem pow_mulShift (Ïˆ : AddChar R R') (n : â„•) : Ïˆ ^ n = mulShift Ïˆ n := by
  ext x
  -- âŠ¢ â†‘(Ïˆ ^ n) x = â†‘(mulShift Ïˆ â†‘n) x
  rw [show (Ïˆ ^ n) x = Ïˆ x ^ n from rfl, â† mulShift_spec']
  -- ğŸ‰ no goals
#align add_char.pow_mul_shift AddChar.pow_mulShift

/-- The product of `mulShift Ïˆ a` and `mulShift Ïˆ b` is `mulShift Ïˆ (a + b)`. -/
theorem mulShift_mul (Ïˆ : AddChar R R') (a b : R) :
    mulShift Ïˆ a * mulShift Ïˆ b = mulShift Ïˆ (a + b) := by
  ext
  -- âŠ¢ â†‘(mulShift Ïˆ a * mulShift Ïˆ b) xâœ = â†‘(mulShift Ïˆ (a + b)) xâœ
  rw [mulShift_apply, right_distrib, map_add_mul]; norm_cast
  -- âŠ¢ â†‘(mulShift Ïˆ a * mulShift Ïˆ b) xâœ = â†‘Ïˆ (a * xâœ) * â†‘Ïˆ (b * xâœ)
                                                   -- ğŸ‰ no goals
#align add_char.mul_shift_mul AddChar.mulShift_mul

/-- `mulShift Ïˆ 0` is the trivial character. -/
@[simp]
theorem mulShift_zero (Ïˆ : AddChar R R') : mulShift Ïˆ 0 = 1 := by
  ext
  -- âŠ¢ â†‘(mulShift Ïˆ 0) xâœ = â†‘1 xâœ
  rw [mulShift_apply, zero_mul, map_zero_one]; norm_cast
  -- âŠ¢ 1 = â†‘1 xâœ
                                               -- ğŸ‰ no goals
#align add_char.mul_shift_zero AddChar.mulShift_zero

/-- An additive character is *primitive* iff all its multiplicative shifts by nonzero
elements are nontrivial. -/
def IsPrimitive (Ïˆ : AddChar R R') : Prop :=
  âˆ€ a : R, a â‰  0 â†’ IsNontrivial (mulShift Ïˆ a)
#align add_char.is_primitive AddChar.IsPrimitive

/-- The map associating to `a : R` the multiplicative shift of `Ïˆ` by `a`
is injective when `Ïˆ` is primitive. -/
theorem to_mulShift_inj_of_isPrimitive {Ïˆ : AddChar R R'} (hÏˆ : IsPrimitive Ïˆ) :
    Function.Injective Ïˆ.mulShift := by
  intro a b h
  -- âŠ¢ a = b
  apply_fun fun x => x * mulShift Ïˆ (-b) at h
  -- âŠ¢ a = b
  simp only [mulShift_mul, mulShift_zero, add_right_neg] at h
  -- âŠ¢ a = b
  have hâ‚‚ := hÏˆ (a + -b)
  -- âŠ¢ a = b
  rw [h, isNontrivial_iff_ne_trivial, â† sub_eq_add_neg, sub_ne_zero] at hâ‚‚
  -- âŠ¢ a = b
  exact not_not.mp fun h => hâ‚‚ h rfl
  -- ğŸ‰ no goals
#align add_char.to_mul_shift_inj_of_is_primitive AddChar.to_mulShift_inj_of_isPrimitive

-- `AddCommGroup.equiv_direct_sum_zmod_of_fintype`
-- gives the structure theorem for finite abelian groups.
-- This could be used to show that the map above is a bijection.
-- We leave this for a later occasion.
/-- When `R` is a field `F`, then a nontrivial additive character is primitive -/
theorem IsNontrivial.isPrimitive {F : Type u} [Field F] {Ïˆ : AddChar F R'} (hÏˆ : IsNontrivial Ïˆ) :
    IsPrimitive Ïˆ := by
  intro a ha
  -- âŠ¢ IsNontrivial (mulShift Ïˆ a)
  cases' hÏˆ with x h
  -- âŠ¢ IsNontrivial (mulShift Ïˆ a)
  use aâ»Â¹ * x
  -- âŠ¢ â†‘(mulShift Ïˆ a) (aâ»Â¹ * x) â‰  1
  rwa [mulShift_apply, mul_inv_cancel_leftâ‚€ ha]
  -- ğŸ‰ no goals
#align add_char.is_nontrivial.is_primitive AddChar.IsNontrivial.isPrimitive

-- Porting note: Using `structure` gives a timeout, see
-- https://leanprover.zulipchat.com/#narrow/stream/287929-mathlib4/topic/mysterious.20finsupp.20related.20timeout/near/365719262 and
-- https://leanprover.zulipchat.com/#narrow/stream/287929-mathlib4/topic/mysterious.20finsupp.20related.20timeout
-- In Lean4, `set_option genInjectivity false in` may solve this issue.
-- can't prove that they always exist
/-- Definition for a primitive additive character on a finite ring `R` into a cyclotomic extension
of a field `R'`. It records which cyclotomic extension it is, the character, and the
fact that the character is primitive. -/
-- @[nolint has_nonempty_instance] -- Porting note: removed
def PrimitiveAddChar (R : Type u) [CommRing R] (R' : Type v) [Field R'] :=
  Î£ n : â„•+, Î£' char : AddChar R (CyclotomicField n R'), IsPrimitive char
#align add_char.primitive_add_char AddChar.PrimitiveAddChar

/-- The first projection from `PrimitiveAddChar`, giving the cyclotomic field. -/
noncomputable def PrimitiveAddChar.n {R : Type u} [CommRing R] {R' : Type v} [Field R'] :
    PrimitiveAddChar R R' â†’ â„•+ := fun Ï‡ => Ï‡.1
#align add_char.primitive_add_char.n AddChar.PrimitiveAddChar.n

/-- The second projection from `PrimitiveAddChar`, giving the character. -/
noncomputable def PrimitiveAddChar.char {R : Type u} [CommRing R] {R' : Type v} [Field R'] :
    âˆ€ Ï‡ : PrimitiveAddChar R R', AddChar R (CyclotomicField Ï‡.n R') := fun Ï‡ => Ï‡.2.1
#align add_char.primitive_add_char.char AddChar.PrimitiveAddChar.char

/-- The third projection from `PrimitiveAddChar`, showing that `Ï‡.2` is primitive. -/
theorem PrimitiveAddChar.prim {R : Type u} [CommRing R] {R' : Type v} [Field R'] :
    âˆ€ Ï‡ : PrimitiveAddChar R R', IsPrimitive Ï‡.char := fun Ï‡ => Ï‡.2.2
#align add_char.primitive_add_char.prim AddChar.PrimitiveAddChar.prim

/-!
### Additive characters on `ZMod n`
-/


variable {C : Type v} [CommRing C]

section ZModCharDef

open Multiplicative

-- so we can write simply `toAdd`, which we need here again
/-- We can define an additive character on `ZMod n` when we have an `n`th root of unity `Î¶ : C`. -/
def zmodChar (n : â„•+) {Î¶ : C} (hÎ¶ : Î¶ ^ (n : â„•) = 1) : AddChar (ZMod n) C where
  toFun := fun a : Multiplicative (ZMod n) => Î¶ ^ a.toAdd.val
  map_one' := by simp only [toAdd_one, ZMod.val_zero, pow_zero]
                 -- ğŸ‰ no goals
  map_mul' x y := by
    dsimp only
    -- âŠ¢ Î¶ ^ ZMod.val (â†‘toAdd (x * y)) = Î¶ ^ ZMod.val (â†‘toAdd x) * Î¶ ^ ZMod.val (â†‘toA â€¦
    rw [toAdd_mul, â† pow_add, ZMod.val_add (toAdd x) (toAdd y), â† pow_eq_pow_mod _ hÎ¶]
    -- ğŸ‰ no goals
#align add_char.zmod_char AddChar.zmodChar

/-- The additive character on `ZMod n` defined using `Î¶` sends `a` to `Î¶^a`. -/
theorem zmodChar_apply {n : â„•+} {Î¶ : C} (hÎ¶ : Î¶ ^ (n : â„•) = 1) (a : ZMod n) :
    zmodChar n hÎ¶ a = Î¶ ^ a.val :=
  rfl
#align add_char.zmod_char_apply AddChar.zmodChar_apply

theorem zmodChar_apply' {n : â„•+} {Î¶ : C} (hÎ¶ : Î¶ ^ (n : â„•) = 1) (a : â„•) :
    zmodChar n hÎ¶ a = Î¶ ^ a := by
  rw [pow_eq_pow_mod a hÎ¶, zmodChar_apply, ZMod.val_nat_cast a]
  -- ğŸ‰ no goals
#align add_char.zmod_char_apply' AddChar.zmodChar_apply'

end ZModCharDef

/-- An additive character on `ZMod n` is nontrivial iff it takes a value `â‰  1` on `1`. -/
theorem zmod_char_isNontrivial_iff (n : â„•+) (Ïˆ : AddChar (ZMod n) C) :
    IsNontrivial Ïˆ â†” Ïˆ 1 â‰  1 := by
  refine' âŸ¨_, fun h => âŸ¨1, hâŸ©âŸ©
  -- âŠ¢ IsNontrivial Ïˆ â†’ â†‘Ïˆ 1 â‰  1
  contrapose!
  -- âŠ¢ â†‘Ïˆ 1 = 1 â†’ Â¬IsNontrivial Ïˆ
  rintro hâ‚ âŸ¨a, haâŸ©
  -- âŠ¢ False
  have haâ‚ : a = a.val â€¢ (1 : ZMod â†‘n) := by
    rw [nsmul_eq_mul, mul_one]; exact (ZMod.nat_cast_zmod_val a).symm
  rw [haâ‚, map_nsmul_pow, hâ‚, one_pow] at ha
  -- âŠ¢ False
  exact ha rfl
  -- ğŸ‰ no goals
#align add_char.zmod_char_is_nontrivial_iff AddChar.zmod_char_isNontrivial_iff

/-- A primitive additive character on `ZMod n` takes the value `1` only at `0`. -/
theorem IsPrimitive.zmod_char_eq_one_iff (n : â„•+) {Ïˆ : AddChar (ZMod n) C} (hÏˆ : IsPrimitive Ïˆ)
    (a : ZMod n) : Ïˆ a = 1 â†” a = 0 := by
  refine' âŸ¨fun h => not_imp_comm.mp (hÏˆ a) _, fun ha => by rw [ha, map_zero_one]âŸ©
  -- âŠ¢ Â¬IsNontrivial (mulShift Ïˆ a)
  rw [zmod_char_isNontrivial_iff n (mulShift Ïˆ a), mulShift_apply, mul_one, h, Classical.not_not]
  -- ğŸ‰ no goals
#align add_char.is_primitive.zmod_char_eq_one_iff AddChar.IsPrimitive.zmod_char_eq_one_iff

/-- The converse: if the additive character takes the value `1` only at `0`,
then it is primitive. -/
theorem zmod_char_primitive_of_eq_one_only_at_zero (n : â„•) (Ïˆ : AddChar (ZMod n) C)
    (hÏˆ : âˆ€ a, Ïˆ a = 1 â†’ a = 0) : IsPrimitive Ïˆ := by
  refine' fun a ha => (isNontrivial_iff_ne_trivial _).mpr fun hf => _
  -- âŠ¢ False
  have h : mulShift Ïˆ a 1 = (1 : AddChar (ZMod n) C) (1 : ZMod n) :=
    congr_fun (congr_arg (â†‘) hf) 1
  rw [mulShift_apply, mul_one] at h; norm_cast at h
  -- âŠ¢ False
                                     -- âŠ¢ False
  exact ha (hÏˆ a h)
  -- ğŸ‰ no goals
#align add_char.zmod_char_primitive_of_eq_one_only_at_zero AddChar.zmod_char_primitive_of_eq_one_only_at_zero

/-- The additive character on `ZMod n` associated to a primitive `n`th root of unity
is primitive -/
theorem zmodChar_primitive_of_primitive_root (n : â„•+) {Î¶ : C} (h : IsPrimitiveRoot Î¶ n) :
    IsPrimitive (zmodChar n ((IsPrimitiveRoot.iff_def Î¶ n).mp h).left) := by
  apply zmod_char_primitive_of_eq_one_only_at_zero
  -- âŠ¢ âˆ€ (a : ZMod â†‘n), â†‘(zmodChar n (_ : Î¶ ^ â†‘n = 1)) a = 1 â†’ a = 0
  intro a ha
  -- âŠ¢ a = 0
  rw [zmodChar_apply, â† pow_zero Î¶] at ha
  -- âŠ¢ a = 0
  exact (ZMod.val_eq_zero a).mp (IsPrimitiveRoot.pow_inj h (ZMod.val_lt a) n.pos ha)
  -- ğŸ‰ no goals
#align add_char.zmod_char_primitive_of_primitive_root AddChar.zmodChar_primitive_of_primitive_root

/-- There is a primitive additive character on `ZMod n` if the characteristic of the target
does not divide `n` -/
noncomputable def primitiveZModChar (n : â„•+) (F' : Type v) [Field F'] (h : (n : F') â‰  0) :
    PrimitiveAddChar (ZMod n) F' :=
  haveI : NeZero ((n : â„•) : F') := âŸ¨hâŸ©
  âŸ¨n, zmodChar n (IsCyclotomicExtension.zeta_pow n F' _),
    zmodChar_primitive_of_primitive_root n (IsCyclotomicExtension.zeta_spec n F' _)âŸ©
#align add_char.primitive_zmod_char AddChar.primitiveZModChar

/-!
### Existence of a primitive additive character on a finite field
-/


/-- There is a primitive additive character on the finite field `F` if the characteristic
of the target is different from that of `F`.
We obtain it as the composition of the trace from `F` to `ZMod p` with a primitive
additive character on `ZMod p`, where `p` is the characteristic of `F`. -/
noncomputable def primitiveCharFiniteField (F F' : Type*) [Field F] [Fintype F] [Field F']
    (h : ringChar F' â‰  ringChar F) : PrimitiveAddChar F F' := by
  let p := ringChar F
  -- âŠ¢ PrimitiveAddChar F F'
  haveI hp : Fact p.Prime := âŸ¨CharP.char_is_prime F _âŸ©
  -- âŠ¢ PrimitiveAddChar F F'
  let pp := p.toPNat hp.1.pos
  -- âŠ¢ PrimitiveAddChar F F'
  have hpâ‚‚ : Â¬ringChar F' âˆ£ p := by
    cases' CharP.char_is_prime_or_zero F' (ringChar F') with hq hq
    Â· exact mt (Nat.Prime.dvd_iff_eq hp.1 (Nat.Prime.ne_one hq)).mp h.symm
    Â· rw [hq]
      exact fun hf => Nat.Prime.ne_zero hp.1 (zero_dvd_iff.mp hf)
  let Ïˆ := primitiveZModChar pp F' (neZero_iff.mp (NeZero.of_not_dvd F' hpâ‚‚))
  -- âŠ¢ PrimitiveAddChar F F'
  letI : Algebra (ZMod p) F := ZMod.algebra _ _
  -- âŠ¢ PrimitiveAddChar F F'
  let Ïˆ' := Ïˆ.char.comp (AddMonoidHom.toMultiplicative (Algebra.trace (ZMod p) F).toAddMonoidHom)
  -- âŠ¢ PrimitiveAddChar F F'
  have hÏˆ' : IsNontrivial Ïˆ' := by
    obtain âŸ¨a, haâŸ© := FiniteField.trace_to_zmod_nondegenerate F one_ne_zero
    rw [one_mul] at ha
    exact âŸ¨a, fun hf => ha <| (Ïˆ.prim.zmod_char_eq_one_iff pp <| Algebra.trace (ZMod p) F a).mp hfâŸ©
  exact âŸ¨Ïˆ.n, Ïˆ', hÏˆ'.isPrimitiveâŸ©
  -- ğŸ‰ no goals
#align add_char.primitive_char_finite_field AddChar.primitiveCharFiniteField

/-!
### The sum of all character values
-/


open scoped BigOperators

variable [Fintype R]

/-- The sum over the values of a nontrivial additive character vanishes if the target ring
is a domain. -/
theorem sum_eq_zero_of_isNontrivial [IsDomain R'] {Ïˆ : AddChar R R'} (hÏˆ : IsNontrivial Ïˆ) :
    âˆ‘ a, Ïˆ a = 0 := by
  rcases hÏˆ with âŸ¨b, hbâŸ©
  -- âŠ¢ âˆ‘ a : R, â†‘Ïˆ a = 0
  have hâ‚ : âˆ‘ a : R, Ïˆ (b + a) = âˆ‘ a : R, Ïˆ a :=
    Fintype.sum_bijective _ (AddGroup.addLeft_bijective b) _ _ fun x => rfl
  simp_rw [map_add_mul] at hâ‚
  -- âŠ¢ âˆ‘ a : R, â†‘Ïˆ a = 0
  have hâ‚‚ : âˆ‘ a : R, Ïˆ a = Finset.univ.sum â†‘Ïˆ := rfl
  -- âŠ¢ âˆ‘ a : R, â†‘Ïˆ a = 0
  rw [â† Finset.mul_sum, hâ‚‚] at hâ‚
  -- âŠ¢ âˆ‘ a : R, â†‘Ïˆ a = 0
  exact eq_zero_of_mul_eq_self_left hb hâ‚
  -- ğŸ‰ no goals
#align add_char.sum_eq_zero_of_is_nontrivial AddChar.sum_eq_zero_of_isNontrivial

/-- The sum over the values of the trivial additive character is the cardinality of the source. -/
theorem sum_eq_card_of_is_trivial {Ïˆ : AddChar R R'} (hÏˆ : Â¬IsNontrivial Ïˆ) :
    âˆ‘ a, Ïˆ a = Fintype.card R := by
  simp only [IsNontrivial] at hÏˆ
  -- âŠ¢ âˆ‘ a : R, â†‘Ïˆ a = â†‘(Fintype.card R)
  push_neg at hÏˆ
  -- âŠ¢ âˆ‘ a : R, â†‘Ïˆ a = â†‘(Fintype.card R)
  simp only [hÏˆ, Finset.sum_const, Nat.smul_one_eq_coe]
  -- âŠ¢ â†‘(Finset.card Finset.univ) = â†‘(Fintype.card R)
  rfl
  -- ğŸ‰ no goals
#align add_char.sum_eq_card_of_is_trivial AddChar.sum_eq_card_of_is_trivial

/-- The sum over the values of `mulShift Ïˆ b` for `Ïˆ` primitive is zero when `b â‰  0`
and `#R` otherwise. -/
theorem sum_mulShift [DecidableEq R] [IsDomain R'] {Ïˆ : AddChar R R'} (b : R)
    (hÏˆ : IsPrimitive Ïˆ) : âˆ‘ x : R, Ïˆ (x * b) = if b = 0 then Fintype.card R else 0 := by
  split_ifs with h
  -- âŠ¢ âˆ‘ x : R, â†‘Ïˆ (x * b) = â†‘(Fintype.card R)
  Â· -- case `b = 0`
    simp only [h, mul_zero, map_zero_one, Finset.sum_const, Nat.smul_one_eq_coe]
    -- âŠ¢ â†‘(Finset.card Finset.univ) = â†‘(Fintype.card R)
    rfl
    -- ğŸ‰ no goals
  Â· -- case `b â‰  0`
    simp_rw [mul_comm]
    -- âŠ¢ âˆ‘ x : R, â†‘Ïˆ (b * x) = â†‘0
    exact_mod_cast sum_eq_zero_of_isNontrivial (hÏˆ b h)
    -- ğŸ‰ no goals
#align add_char.sum_mul_shift AddChar.sum_mulShift

end Additive

end AddChar
