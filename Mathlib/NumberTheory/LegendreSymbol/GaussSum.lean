/-
Copyright (c) 2022 Michael Stoll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Stoll
-/
import Mathlib.NumberTheory.LegendreSymbol.AddCharacter
import Mathlib.NumberTheory.LegendreSymbol.ZModChar
import Mathlib.Algebra.CharP.CharAndCard

#align_import number_theory.legendre_symbol.gauss_sum from "leanprover-community/mathlib"@"e3f4be1fcb5376c4948d7f095bec45350bfb9d1a"

/-!
# Gauss sums

We define the Gauss sum associated to a multiplicative and an additive
character of a finite field and prove some results about them.

## Main definition

Let `R` be a finite commutative ring and let `R'` be another commutative ring.
If `Ï‡` is a multiplicative character `R â†’ R'` (type `MulChar R R'`) and `Ïˆ`
is an additive character `R â†’ R'` (type `AddChar R R'`, which abbreviates
`(Multiplicative R) â†’* R'`), then the *Gauss sum* of `Ï‡` and `Ïˆ` is `âˆ‘ a, Ï‡ a * Ïˆ a`.

## Main results

Some important results are as follows.

* `gaussSum_mul_gaussSum_eq_card`: The product of the Gauss
  sums of `Ï‡` and `Ïˆ` and that of `Ï‡â»Â¹` and `Ïˆâ»Â¹` is the cardinality
  of the source ring `R` (if `Ï‡` is nontrivial, `Ïˆ` is primitive and `R` is a field).
* `gaussSum_sq`: The square of the Gauss sum is `Ï‡(-1)` times
  the cardinality of `R` if in addition `Ï‡` is a quadratic character.
* `MulChar.IsQuadratic.gaussSum_frob`: For a quadratic character `Ï‡`, raising
  the Gauss sum to the `p`th power (where `p` is the characteristic of
  the target ring `R'`) multiplies it by `Ï‡ p`.
* `Char.card_pow_card`: When `F` and `F'` are finite fields and `Ï‡ : F â†’ F'`
  is a nontrivial quadratic character, then `(Ï‡ (-1) * #F)^(#F'/2) = Ï‡ #F'`.
* `FiniteField.two_pow_card`: For every finite field `F` of odd characteristic,
  we have `2^(#F/2) = Ï‡â‚ˆ#F` in `F`.

This machinery can be used to derive (a generalization of) the Law of
Quadratic Reciprocity.

## Tags

additive character, multiplicative character, Gauss sum
-/


universe u v

open scoped BigOperators

open AddChar MulChar

section GaussSumDef

-- `R` is the domain of the characters
variable {R : Type u} [CommRing R] [Fintype R]

-- `R'` is the target of the characters
variable {R' : Type v} [CommRing R']

/-!
### Definition and first properties
-/


/-- Definition of the Gauss sum associated to a multiplicative and an additive character. -/
def gaussSum (Ï‡ : MulChar R R') (Ïˆ : AddChar R R') : R' :=
  âˆ‘ a, Ï‡ a * Ïˆ a
#align gauss_sum gaussSum

/-- Replacing `Ïˆ` by `mulShift Ïˆ a` and multiplying the Gauss sum by `Ï‡ a` does not change it. -/
theorem gaussSum_mulShift (Ï‡ : MulChar R R') (Ïˆ : AddChar R R') (a : RË£) :
    Ï‡ a * gaussSum Ï‡ (mulShift Ïˆ a) = gaussSum Ï‡ Ïˆ := by
  simp only [gaussSum, mulShift_apply, Finset.mul_sum]
  -- âŠ¢ âˆ‘ x : R, â†‘Ï‡ â†‘a * (â†‘Ï‡ x * â†‘Ïˆ (â†‘a * x)) = âˆ‘ x : R, â†‘Ï‡ x * â†‘Ïˆ x
  simp_rw [â† mul_assoc, â† map_mul]
  -- âŠ¢ âˆ‘ x : R, â†‘Ï‡ (â†‘a * x) * â†‘Ïˆ (â†‘a * x) = âˆ‘ x : R, â†‘Ï‡ x * â†‘Ïˆ x
  exact Fintype.sum_bijective _ a.mulLeft_bijective _ _ fun x => rfl
  -- ğŸ‰ no goals
#align gauss_sum_mul_shift gaussSum_mulShift

end GaussSumDef

/-!
### The product of two Gauss sums
-/


section GaussSumProd

-- In the following, we need `R` to be a finite field and `R'` to be a domain.
variable {R : Type u} [Field R] [Fintype R] {R' : Type v} [CommRing R'] [IsDomain R']

-- A helper lemma for `gaussSum_mul_gaussSum_eq_card` below
-- Is this useful enough in other contexts to be public?
private theorem gaussSum_mul_aux {Ï‡ : MulChar R R'} (hÏ‡ : IsNontrivial Ï‡) (Ïˆ : AddChar R R')
    (b : R) : âˆ‘ a, Ï‡ (a * bâ»Â¹) * Ïˆ (a - b) = âˆ‘ c, Ï‡ c * Ïˆ (b * (c - 1)) := by
  cases' eq_or_ne b 0 with hb hb
  -- âŠ¢ âˆ‘ a : R, â†‘Ï‡ (a * bâ»Â¹) * â†‘Ïˆ (a - b) = âˆ‘ c : R, â†‘Ï‡ c * â†‘Ïˆ (b * (c - 1))
  Â· -- case `b = 0`
    simp only [hb, inv_zero, mul_zero, MulChar.map_zero, zero_mul,
      Finset.sum_const_zero, map_zero_one, mul_one]
    exact (hÏ‡.sum_eq_zero).symm
    -- ğŸ‰ no goals
  Â· -- case `b â‰  0`
    refine' (Fintype.sum_bijective _ (mulLeft_bijectiveâ‚€ b hb) _ _ fun x => _).symm
    -- âŠ¢ â†‘Ï‡ x * â†‘Ïˆ (b * (x - 1)) = â†‘Ï‡ ((fun x x_1 => x * x_1) b x * bâ»Â¹) * â†‘Ïˆ ((fun x â€¦
    rw [mul_assoc, mul_comm x, â† mul_assoc, mul_inv_cancel hb, one_mul, mul_sub, mul_one]
    -- ğŸ‰ no goals

/-- We have `gaussSum Ï‡ Ïˆ * gaussSum Ï‡â»Â¹ Ïˆâ»Â¹ = Fintype.card R`
when `Ï‡` is nontrivial and `Ïˆ` is primitive (and `R` is a field). -/
theorem gaussSum_mul_gaussSum_eq_card {Ï‡ : MulChar R R'} (hÏ‡ : IsNontrivial Ï‡) {Ïˆ : AddChar R R'}
    (hÏˆ : IsPrimitive Ïˆ) : gaussSum Ï‡ Ïˆ * gaussSum Ï‡â»Â¹ Ïˆâ»Â¹ = Fintype.card R := by
  simp only [gaussSum, AddChar.inv_apply, Finset.sum_mul, Finset.mul_sum, MulChar.inv_apply']
  -- âŠ¢ âˆ‘ x : R, âˆ‘ x_1 : R, â†‘Ï‡ x_1 * â†‘Ïˆ x_1 * (â†‘Ï‡ xâ»Â¹ * â†‘Ïˆ (-x)) = â†‘(Fintype.card R)
  conv =>
    lhs; congr; next => skip
    ext; congr; next => skip
    ext
    rw [mul_mul_mul_comm, â† map_mul, â† map_add_mul, â† sub_eq_add_neg]
--  conv in _ * _ * (_ * _) => rw [mul_mul_mul_comm, â† map_mul, â† map_add_mul, â† sub_eq_add_neg]
  simp_rw [gaussSum_mul_aux hÏ‡ Ïˆ]
  -- âŠ¢ âˆ‘ x : R, âˆ‘ x_1 : R, â†‘Ï‡ x_1 * â†‘Ïˆ (x * (x_1 - 1)) = â†‘(Fintype.card R)
  rw [Finset.sum_comm]
  -- âŠ¢ âˆ‘ y : R, âˆ‘ x : R, â†‘Ï‡ y * â†‘Ïˆ (x * (y - 1)) = â†‘(Fintype.card R)
  classical -- to get `[DecidableEq R]` for `sum_mulShift`
  simp_rw [â† Finset.mul_sum, sum_mulShift _ hÏˆ, sub_eq_zero, apply_ite, Nat.cast_zero, mul_zero]
  rw [Finset.sum_ite_eq' Finset.univ (1 : R)]
  simp only [Finset.mem_univ, map_one, one_mul, if_true]
#align gauss_sum_mul_gauss_sum_eq_card gaussSum_mul_gaussSum_eq_card

/-- When `Ï‡` is a nontrivial quadratic character, then the square of `gaussSum Ï‡ Ïˆ`
is `Ï‡(-1)` times the cardinality of `R`. -/
theorem gaussSum_sq {Ï‡ : MulChar R R'} (hÏ‡â‚ : IsNontrivial Ï‡) (hÏ‡â‚‚ : IsQuadratic Ï‡)
    {Ïˆ : AddChar R R'} (hÏˆ : IsPrimitive Ïˆ) : gaussSum Ï‡ Ïˆ ^ 2 = Ï‡ (-1) * Fintype.card R := by
  rw [pow_two, â† gaussSum_mul_gaussSum_eq_card hÏ‡â‚ hÏˆ, hÏ‡â‚‚.inv, mul_rotate']
  -- âŠ¢ gaussSum Ï‡ Ïˆ * gaussSum Ï‡ Ïˆ = gaussSum Ï‡ Ïˆ * (gaussSum Ï‡ Ïˆâ»Â¹ * â†‘Ï‡ (-1))
  congr
  -- âŠ¢ gaussSum Ï‡ Ïˆ = gaussSum Ï‡ Ïˆâ»Â¹ * â†‘Ï‡ (-1)
  rw [mul_comm, â† gaussSum_mulShift _ _ (-1 : RË£), inv_mulShift]
  -- âŠ¢ â†‘Ï‡ â†‘(-1) * gaussSum Ï‡ (mulShift Ïˆ â†‘(-1)) = â†‘Ï‡ (-1) * gaussSum Ï‡ (mulShift Ïˆ  â€¦
  rfl
  -- ğŸ‰ no goals
#align gauss_sum_sq gaussSum_sq

end GaussSumProd

/-!
### Gauss sums and Frobenius
-/


section gaussSum_frob

variable {R : Type u} [CommRing R] [Fintype R] {R' : Type v} [CommRing R']

-- We assume that the target ring `R'` has prime characteristic `p`.
variable (p : â„•) [fp : Fact p.Prime] [hch : CharP R' p]

/-- When `R'` has prime characteristic `p`, then the `p`th power of the Gauss sum
of `Ï‡` and `Ïˆ` is the Gauss sum of `Ï‡^p` and `Ïˆ^p`. -/
theorem gaussSum_frob (Ï‡ : MulChar R R') (Ïˆ : AddChar R R') :
    gaussSum Ï‡ Ïˆ ^ p = gaussSum (Ï‡ ^ p) (Ïˆ ^ p) := by
  rw [â† frobenius_def, gaussSum, gaussSum, map_sum]
  -- âŠ¢ âˆ‘ x : R, â†‘(frobenius R' p) (â†‘Ï‡ x * â†‘Ïˆ x) = âˆ‘ a : R, â†‘(Ï‡ ^ p) a * â†‘(Ïˆ ^ p) a
  simp_rw [pow_apply' Ï‡ fp.1.pos, map_mul, frobenius_def]
  -- âŠ¢ âˆ‘ x : R, â†‘Ï‡ x ^ p * â†‘Ïˆ x ^ p = âˆ‘ x : R, â†‘Ï‡ x ^ p * â†‘(Ïˆ ^ p) x
  rfl
  -- ğŸ‰ no goals
#align gauss_sum_frob gaussSum_frob

/-- For a quadratic character `Ï‡` and when the characteristic `p` of the target ring
is a unit in the source ring, the `p`th power of the Gauss sum of`Ï‡` and `Ïˆ` is
`Ï‡ p` times the original Gauss sum. -/
-- Porting note: Added `nonrec` to avoid error `failed to prove termination`
nonrec theorem MulChar.IsQuadratic.gaussSum_frob (hp : IsUnit (p : R)) {Ï‡ : MulChar R R'}
    (hÏ‡ : IsQuadratic Ï‡) (Ïˆ : AddChar R R') : gaussSum Ï‡ Ïˆ ^ p = Ï‡ p * gaussSum Ï‡ Ïˆ := by
  rw [gaussSum_frob, pow_mulShift, hÏ‡.pow_char p, â† gaussSum_mulShift Ï‡ Ïˆ hp.unit, â† mul_assoc,
    hp.unit_spec, â† pow_two, â† pow_apply' _ (by norm_num : 0 < 2), hÏ‡.sq_eq_one, â† hp.unit_spec,
    one_apply_coe, one_mul]
#align mul_char.is_quadratic.gauss_sum_frob MulChar.IsQuadratic.gaussSum_frob

/-- For a quadratic character `Ï‡` and when the characteristic `p` of the target ring
is a unit in the source ring and `n` is a natural number, the `p^n`th power of the Gauss
sum of`Ï‡` and `Ïˆ` is `Ï‡ (p^n)` times the original Gauss sum. -/
theorem MulChar.IsQuadratic.gaussSum_frob_iter (n : â„•) (hp : IsUnit (p : R)) {Ï‡ : MulChar R R'}
    (hÏ‡ : IsQuadratic Ï‡) (Ïˆ : AddChar R R') :
    gaussSum Ï‡ Ïˆ ^ p ^ n = Ï‡ ((p : R) ^ n) * gaussSum Ï‡ Ïˆ := by
  induction' n with n ih
  -- âŠ¢ gaussSum Ï‡ Ïˆ ^ p ^ Nat.zero = â†‘Ï‡ (â†‘p ^ Nat.zero) * gaussSum Ï‡ Ïˆ
  Â· rw [pow_zero, pow_one, pow_zero, MulChar.map_one, one_mul]
    -- ğŸ‰ no goals
  Â· rw [pow_succ, mul_comm p, pow_mul, ih, mul_pow, hÏ‡.gaussSum_frob _ hp, â† mul_assoc, pow_succ,
      mul_comm (p : R), map_mul, â† pow_apply' Ï‡ fp.1.pos ((p : R) ^ n), hÏ‡.pow_char p]
#align mul_char.is_quadratic.gauss_sum_frob_iter MulChar.IsQuadratic.gaussSum_frob_iter

end gaussSum_frob

/-!
### Values of quadratic characters
-/


section GaussSumValues

variable {R : Type u} [CommRing R] [Fintype R] {R' : Type v} [CommRing R'] [IsDomain R']

/-- If the square of the Gauss sum of a quadratic character is `Ï‡(-1) * #R`,
then we get, for all `n : â„•`, the relation `(Ï‡(-1) * #R) ^ (p^n/2) = Ï‡(p^n)`,
where `p` is the (odd) characteristic of the target ring `R'`.
This version can be used when `R` is not a field, e.g., `â„¤/8â„¤`. -/
theorem Char.card_pow_char_pow {Ï‡ : MulChar R R'} (hÏ‡ : IsQuadratic Ï‡) (Ïˆ : AddChar R R') (p n : â„•)
    [fp : Fact p.Prime] [hch : CharP R' p] (hp : IsUnit (p : R)) (hp' : p â‰  2)
    (hg : gaussSum Ï‡ Ïˆ ^ 2 = Ï‡ (-1) * Fintype.card R) :
    (Ï‡ (-1) * Fintype.card R) ^ (p ^ n / 2) = Ï‡ ((p : R) ^ n) := by
  have : gaussSum Ï‡ Ïˆ â‰  0 := by
    intro hf; rw [hf, zero_pow (by norm_num : 0 < 2), eq_comm, mul_eq_zero] at hg
    exact not_isUnit_prime_of_dvd_card p
        ((CharP.cast_eq_zero_iff R' p _).mp <| hg.resolve_left (isUnit_one.neg.map Ï‡).ne_zero) hp
  rw [â† hg]; apply mul_right_cancelâ‚€ this
  -- âŠ¢ (gaussSum Ï‡ Ïˆ ^ 2) ^ (p ^ n / 2) = â†‘Ï‡ (â†‘p ^ n)
             -- âŠ¢ (gaussSum Ï‡ Ïˆ ^ 2) ^ (p ^ n / 2) * gaussSum Ï‡ Ïˆ = â†‘Ï‡ (â†‘p ^ n) * gaussSum Ï‡ Ïˆ
  rw [â† hÏ‡.gaussSum_frob_iter p n hp Ïˆ, â† pow_mul, mul_comm, â† pow_succ,
    Nat.two_mul_div_two_add_one_of_odd (fp.1.eq_two_or_odd'.resolve_left hp').pow]
#align char.card_pow_char_pow Char.card_pow_char_pow

/-- When `F` and `F'` are finite fields and `Ï‡ : F â†’ F'` is a nontrivial quadratic character,
then `(Ï‡(-1) * #F)^(#F'/2) = Ï‡(#F')`. -/
theorem Char.card_pow_card {F : Type*} [Field F] [Fintype F] {F' : Type*} [Field F'] [Fintype F']
    {Ï‡ : MulChar F F'} (hÏ‡â‚ : IsNontrivial Ï‡) (hÏ‡â‚‚ : IsQuadratic Ï‡)
    (hchâ‚ : ringChar F' â‰  ringChar F) (hchâ‚‚ : ringChar F' â‰  2) :
    (Ï‡ (-1) * Fintype.card F) ^ (Fintype.card F' / 2) = Ï‡ (Fintype.card F') := by
  obtain âŸ¨n, hp, hcâŸ© := FiniteField.card F (ringChar F)
  -- âŠ¢ (â†‘Ï‡ (-1) * â†‘(Fintype.card F)) ^ (Fintype.card F' / 2) = â†‘Ï‡ â†‘(Fintype.card F')
  obtain âŸ¨n', hp', hc'âŸ© := FiniteField.card F' (ringChar F')
  -- âŠ¢ (â†‘Ï‡ (-1) * â†‘(Fintype.card F)) ^ (Fintype.card F' / 2) = â†‘Ï‡ â†‘(Fintype.card F')
  let Ïˆ := primitiveCharFiniteField F F' hchâ‚
  -- âŠ¢ (â†‘Ï‡ (-1) * â†‘(Fintype.card F)) ^ (Fintype.card F' / 2) = â†‘Ï‡ â†‘(Fintype.card F')
  -- Porting note: this was a `let` but then Lean would time out at
  -- unification so it is changed to as `set` and `FF'` is replaced by its
  -- definition before unification
  set FF' := CyclotomicField Ïˆ.n F' with FF'_def
  -- âŠ¢ (â†‘Ï‡ (-1) * â†‘(Fintype.card F)) ^ (Fintype.card F' / 2) = â†‘Ï‡ â†‘(Fintype.card F')
  have hchar := Algebra.ringChar_eq F' FF'
  -- âŠ¢ (â†‘Ï‡ (-1) * â†‘(Fintype.card F)) ^ (Fintype.card F' / 2) = â†‘Ï‡ â†‘(Fintype.card F')
  apply (algebraMap F' FF').injective
  -- âŠ¢ â†‘(algebraMap F' FF') ((â†‘Ï‡ (-1) * â†‘(Fintype.card F)) ^ (Fintype.card F' / 2)) â€¦
  rw [map_pow, map_mul, map_natCast, hc', hchar, Nat.cast_pow]
  -- âŠ¢ (â†‘(algebraMap F' FF') (â†‘Ï‡ (-1)) * â†‘(Fintype.card F)) ^ (ringChar FF' ^ â†‘n' / â€¦
  simp only [â† MulChar.ringHomComp_apply]
  -- âŠ¢ (â†‘(ringHomComp Ï‡ (algebraMap F' (CyclotomicField (PrimitiveAddChar.n (primit â€¦
  haveI := Fact.mk hp'
  -- âŠ¢ (â†‘(ringHomComp Ï‡ (algebraMap F' (CyclotomicField (PrimitiveAddChar.n (primit â€¦
  haveI := Fact.mk (hchar.subst hp')
  -- âŠ¢ (â†‘(ringHomComp Ï‡ (algebraMap F' (CyclotomicField (PrimitiveAddChar.n (primit â€¦
  rw [Ne, â† Nat.prime_dvd_prime_iff_eq hp' hp, â† isUnit_iff_not_dvd_char, hchar] at hchâ‚
  -- âŠ¢ (â†‘(ringHomComp Ï‡ (algebraMap F' (CyclotomicField (PrimitiveAddChar.n (primit â€¦
  -- Porting note: original proof is below and, as noted above, `FF'` needs to
  -- be replaced by its definition before unification to avoid time out
  -- exact Char.card_pow_char_pow (hÏ‡â‚‚.comp _) Ïˆ.char (ringChar FF') n' hchâ‚ (hchar â–¸ hchâ‚‚)
  --      (gaussSum_sq (hÏ‡â‚.comp <| RingHom.injective _) (hÏ‡â‚‚.comp _) Ïˆ.prim)
  have := Char.card_pow_char_pow (hÏ‡â‚‚.comp (algebraMap F' FF')) Ïˆ.char
    (ringChar FF') n' hchâ‚ (hchar â–¸ hchâ‚‚)
    (gaussSum_sq (hÏ‡â‚.comp <| RingHom.injective _) (hÏ‡â‚‚.comp _) Ïˆ.prim)
  simp_rw [FF'_def] at this
  -- âŠ¢ (â†‘(ringHomComp Ï‡ (algebraMap F' (CyclotomicField (PrimitiveAddChar.n (primit â€¦
  exact this
  -- ğŸ‰ no goals
#align char.card_pow_card Char.card_pow_card

end GaussSumValues

section GaussSumTwo

/-!
### The quadratic character of 2

This section proves the following result.

For every finite field `F` of odd characteristic, we have `2^(#F/2) = Ï‡â‚ˆ#F` in `F`.
This can be used to show that the quadratic character of `F` takes the value
`Ï‡â‚ˆ#F` at `2`.

The proof uses the Gauss sum of `Ï‡â‚ˆ` and a primitive additive character on `â„¤/8â„¤`;
in this way, the result is reduced to `card_pow_char_pow`.
-/


open ZMod

-- Porting note: This proof is _really_ slow, maybe it should be broken into several lemmas
-- See https://github.com/leanprover-community/mathlib4/issues/5028
set_option maxHeartbeats 800000 in
/-- For every finite field `F` of odd characteristic, we have `2^(#F/2) = Ï‡â‚ˆ#F` in `F`. -/
theorem FiniteField.two_pow_card {F : Type*} [Fintype F] [Field F] (hF : ringChar F â‰  2) :
    (2 : F) ^ (Fintype.card F / 2) = Ï‡â‚ˆ (Fintype.card F) := by
  have hp2 : âˆ€ n : â„•, (2 ^ n : F) â‰  0 := fun n => pow_ne_zero n (Ring.two_ne_zero hF)
  -- âŠ¢ 2 ^ (Fintype.card F / 2) = â†‘(â†‘Ï‡â‚ˆ â†‘(Fintype.card F))
  obtain âŸ¨n, hp, hcâŸ© := FiniteField.card F (ringChar F)
  -- âŠ¢ 2 ^ (Fintype.card F / 2) = â†‘(â†‘Ï‡â‚ˆ â†‘(Fintype.card F))

  -- we work in `FF`, the eighth cyclotomic field extension of `F`
  -- Porting note: was
  -- let FF := (Polynomial.cyclotomic 8 F).SplittingField
  -- but we want to unify with `CyclotomicField` below.
  let FF := CyclotomicField 8 F
  -- âŠ¢ 2 ^ (Fintype.card F / 2) = â†‘(â†‘Ï‡â‚ˆ â†‘(Fintype.card F))
  haveI : Polynomial.IsSplittingField F FF (Polynomial.cyclotomic 8 F) :=
    Polynomial.IsSplittingField.splittingField _
  haveI : FiniteDimensional F FF :=
    Polynomial.IsSplittingField.finiteDimensional FF (Polynomial.cyclotomic 8 F)
  haveI : Fintype FF := FiniteDimensional.fintypeOfFintype F FF
  -- âŠ¢ 2 ^ (Fintype.card F / 2) = â†‘(â†‘Ï‡â‚ˆ â†‘(Fintype.card F))
  have hchar := Algebra.ringChar_eq F FF
  -- âŠ¢ 2 ^ (Fintype.card F / 2) = â†‘(â†‘Ï‡â‚ˆ â†‘(Fintype.card F))
  have FFp := hchar.subst hp
  -- âŠ¢ 2 ^ (Fintype.card F / 2) = â†‘(â†‘Ï‡â‚ˆ â†‘(Fintype.card F))
  haveI := Fact.mk FFp
  -- âŠ¢ 2 ^ (Fintype.card F / 2) = â†‘(â†‘Ï‡â‚ˆ â†‘(Fintype.card F))
  have hFF := ne_of_eq_of_ne hchar.symm hF
  -- âŠ¢ 2 ^ (Fintype.card F / 2) = â†‘(â†‘Ï‡â‚ˆ â†‘(Fintype.card F))
  -- `ringChar FF â‰  2`
  have hu : IsUnit (ringChar FF : ZMod 8) := by
    rw [isUnit_iff_not_dvd_char, ringChar_zmod_n]
    rw [Ne, â† Nat.prime_dvd_prime_iff_eq FFp Nat.prime_two] at hFF
    change Â¬_ âˆ£ 2 ^ 3
    exact mt FFp.dvd_of_dvd_pow hFF

  -- there is a primitive additive character `â„¤/8â„¤ â†’ FF`, sending `a + 8â„¤ â†¦ Ï„^a`
  -- with a primitive eighth root of unity `Ï„`
  -- Porting note: The type is actually `PrimitiveAddChar (ZMod (8 : â„•+)) F`, but this seems faster.
  let Ïˆâ‚ˆ : PrimitiveAddChar (ZMod 8) F :=
    primitiveZModChar 8 F (by convert hp2 3 using 1; norm_cast)
  -- Porting note: unifying this is very slow, so only do it once.
  let Ïˆâ‚ˆchar : AddChar (ZMod 8) FF := Ïˆâ‚ˆ.char
  -- âŠ¢ 2 ^ (Fintype.card F / 2) = â†‘(â†‘Ï‡â‚ˆ â†‘(Fintype.card F))
  let Ï„ : FF := Ïˆâ‚ˆchar 1
  -- âŠ¢ 2 ^ (Fintype.card F / 2) = â†‘(â†‘Ï‡â‚ˆ â†‘(Fintype.card F))
  have Ï„_spec : Ï„ ^ 4 = -1 := by
    refine (sq_eq_one_iff.1 ?_).resolve_left ?_
    Â· rw [â† pow_mul, â† map_nsmul_pow Ïˆâ‚ˆchar, AddChar.IsPrimitive.zmod_char_eq_one_iff 8 Ïˆâ‚ˆ.prim]
      decide
    Â· rw [â† map_nsmul_pow Ïˆâ‚ˆchar, AddChar.IsPrimitive.zmod_char_eq_one_iff 8 Ïˆâ‚ˆ.prim]
      decide

  -- we consider `Ï‡â‚ˆ` as a multiplicative character `â„¤/8â„¤ â†’ FF`
  let Ï‡ := Ï‡â‚ˆ.ringHomComp (Int.castRingHom FF)
  -- âŠ¢ 2 ^ (Fintype.card F / 2) = â†‘(â†‘Ï‡â‚ˆ â†‘(Fintype.card F))
  have hÏ‡ : Ï‡ (-1) = 1 := Int.cast_one
  -- âŠ¢ 2 ^ (Fintype.card F / 2) = â†‘(â†‘Ï‡â‚ˆ â†‘(Fintype.card F))
  have hq : IsQuadratic Ï‡ := isQuadratic_Ï‡â‚ˆ.comp _
  -- âŠ¢ 2 ^ (Fintype.card F / 2) = â†‘(â†‘Ï‡â‚ˆ â†‘(Fintype.card F))

  -- we now show that the Gauss sum of `Ï‡` and `Ïˆâ‚ˆ` has the relevant property
  have hg : gaussSum Ï‡ Ïˆâ‚ˆchar ^ 2 = Ï‡ (-1) * Fintype.card (ZMod 8) := by
    have _ := congr_arg (Â· ^ 2) (Fin.sum_univ_eight fun x => (Ï‡â‚ˆ x : FF) * Ï„ ^ x.1)
    have hâ‚ : (fun i : Fin 8 => â†‘(Ï‡â‚ˆ i) * Ï„ ^ i.val) = (fun a : ZMod 8 => Ï‡ a * â†‘(Ïˆâ‚ˆchar a)) := by
      -- Porting note: original proof
      -- ext; congr; apply pow_one
      ext (x : Fin 8); rw [â† map_nsmul_pow Ïˆâ‚ˆchar]; congr 2;
      rw [Nat.smul_one_eq_coe, Fin.cast_val_eq_self x]
    have hâ‚‚ : (0 + 1 * Ï„ ^ 1 + 0 + -1 * Ï„ ^ 3 + 0 + -1 * Ï„ ^ 5 + 0 + 1 * Ï„ ^ 7) ^ 2 =
        8 + (Ï„ ^ 4 + 1) * (Ï„ ^ 10 - 2 * Ï„ ^ 8 - 2 * Ï„ ^ 6 + 6 * Ï„ ^ 4 + Ï„ ^ 2 - 8) := by ring
    have hâ‚ƒ : 8 + (Ï„ ^ 4 + 1) * (Ï„ ^ 10 - 2 * Ï„ ^ 8 - 2 * Ï„ ^ 6 + 6 * Ï„ ^ 4 + Ï„ ^ 2 - 8) = â†‘8 := by
      rw [Ï„_spec]; norm_num
    have hâ‚„ : (0 + 1 * Ï„ ^ 1 + 0 + -1 * Ï„ ^ 3 + 0 + -1 * Ï„ ^ 5 + 0 + 1 * Ï„ ^ 7) ^ 2 = â†‘8 := by
      rw [â† hâ‚ƒ, â† hâ‚‚]
    have hâ‚… :
        (â†‘(Ï‡â‚ˆ 0) * Ï„ ^ 0 + â†‘(Ï‡â‚ˆ 1) * Ï„ ^ 1 + â†‘(Ï‡â‚ˆ 2) * Ï„ ^ 2 + â†‘(Ï‡â‚ˆ 3) * Ï„ ^ 3 + â†‘(Ï‡â‚ˆ 4) * Ï„ ^ 4 +
        â†‘(Ï‡â‚ˆ 5) * Ï„ ^ 5 + â†‘(Ï‡â‚ˆ 6) * Ï„ ^ 6 + â†‘(Ï‡â‚ˆ 7) * Ï„ ^ 7) ^ 2 = 8 := by
      -- Porting note: original proof
      --  simp [â† hâ‚„, Ï‡â‚ˆ_apply, Matrix.cons_val_zero, algebraMap.coe_zero, zero_mul,
      -- Matrix.cons_val_one, Matrix.head_cons, algebraMap.coe_one, Matrix.cons_vec_bit0_eq_alt0,
      -- Matrix.cons_vecAppend, Matrix.cons_vecAlt0, Matrix.cons_vec_bit1_eq_alt1,
      -- Matrix.cons_vecAlt1, Int.cast_neg]
      simp_rw [Ï‡â‚ˆ_apply]
      rw [â† hâ‚„]
      dsimp only
      congr
      Â· rw [Matrix.cons_val_zero]; simp
      Â· simp only [Matrix.vecCons, ne_eq, Nat.cast_ofNat, id_eq, eq_mpr_eq_cast, mul_eq_zero,
          zero_lt_two, pow_eq_zero_iff]
        left
        rw [â† Int.cast_zero (R := FF)]
        exact congr_arg Int.cast rfl
      Â· simp only [Matrix.vecCons]
        rw [show (-1 : FF) = â†‘(- 1 : â„¤) by simp only [Int.cast_neg, Int.cast_one]]
        exact congr_arg Int.cast rfl
      Â· simp only [Matrix.vecCons, ne_eq, Nat.cast_ofNat, id_eq, eq_mpr_eq_cast, mul_eq_zero,
          zero_lt_two, pow_eq_zero_iff]
        left
        rw [â† Int.cast_zero (R := FF)]
        exact congr_arg Int.cast rfl
      Â· simp only [Matrix.vecCons]
        rw [show (-1 : FF) = â†‘(- 1 : â„¤) by simp only [Int.cast_neg, Int.cast_one]]
        exact congr_arg Int.cast rfl
      Â· simp only [Matrix.vecCons, ne_eq, Nat.cast_ofNat, id_eq, eq_mpr_eq_cast, mul_eq_zero,
          zero_lt_two, pow_eq_zero_iff]
        left
        rw [â† Int.cast_zero (R := FF)]
        exact congr_arg Int.cast rfl
    -- Porting note: original proof
    -- simpa only [hÏ‡, one_mul, card, gaussSum, â† hâ‚…, hâ‚] using h
    rw [gaussSum, hÏ‡, one_mul, ZMod.card, Nat.cast_ofNat, â† hâ‚…]
    simp_rw [â† hâ‚]
    rw [Fin.sum_univ_eight]
    rfl

  -- this allows us to apply `card_pow_char_pow` to our situation
  have h := Char.card_pow_char_pow (R := ZMod 8) hq Ïˆâ‚ˆchar (ringChar FF) n hu hFF hg
  -- âŠ¢ 2 ^ (Fintype.card F / 2) = â†‘(â†‘Ï‡â‚ˆ â†‘(Fintype.card F))
  rw [ZMod.card, â† hchar, hÏ‡, one_mul, â† hc, â† Nat.cast_pow (ringChar F), â† hc] at h
  -- âŠ¢ 2 ^ (Fintype.card F / 2) = â†‘(â†‘Ï‡â‚ˆ â†‘(Fintype.card F))

  -- finally, we change `2` to `8` on the left hand side
  convert_to (8 : F) ^ (Fintype.card F / 2) = _
  -- âŠ¢ 2 ^ (Fintype.card F / 2) = 8 ^ (Fintype.card F / 2)
  Â· rw [(by norm_num : (8 : F) = 2 ^ 2 * 2), mul_pow,
      (FiniteField.isSquare_iff hF <| hp2 2).mp âŸ¨2, pow_two 2âŸ©, one_mul]
  apply (algebraMap F FF).injective
  -- âŠ¢ â†‘(algebraMap F FF) (8 ^ (Fintype.card F / 2)) = â†‘(algebraMap F FF) â†‘(â†‘Ï‡â‚ˆ â†‘(F â€¦
  simp only [map_pow, map_ofNat, map_intCast]
  -- âŠ¢ 8 ^ (Fintype.card F / 2) = â†‘(â†‘Ï‡â‚ˆ â†‘(Fintype.card F))
  simp only [Nat.cast_ofNat, ringHomComp_apply, eq_intCast] at h
  -- âŠ¢ 8 ^ (Fintype.card F / 2) = â†‘(â†‘Ï‡â‚ˆ â†‘(Fintype.card F))
  exact h
  -- ğŸ‰ no goals
#align finite_field.two_pow_card FiniteField.two_pow_card

end GaussSumTwo
