/-
Copyright (c) 2022 Michael Stoll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Stoll
-/
import Mathlib.Algebra.CharP.Basic
import Mathlib.Algebra.EuclideanDomain.Instances
import Mathlib.Data.Fintype.Units

#align_import number_theory.legendre_symbol.mul_character from "leanprover-community/mathlib"@"f0c8bf9245297a541f468be517f1bde6195105e9"

/-!
# Multiplicative characters of finite rings and fields

Let `R` and `R'` be a commutative rings.
A *multiplicative character* of `R` with values in `R'` is a morphism of
monoids from the multiplicative monoid of `R` into that of `R'`
that sends non-units to zero.

We use the namespace `MulChar` for the definitions and results.

## Main results

We show that the multiplicative characters form a group (if `R'` is commutative);
see `MulChar.commGroup`. We also provide an equivalence with the
homomorphisms `RË£ â†’* R'Ë£`; see `MulChar.equivToUnitHom`.

We define a multiplicative character to be *quadratic* if its values
are among `0`, `1` and `-1`, and we prove some properties of quadratic characters.

Finally, we show that the sum of all values of a nontrivial multiplicative
character vanishes; see `MulChar.IsNontrivial.sum_eq_zero`.

## Tags

multiplicative character
-/


section DefinitionAndGroup

/-!
### Definitions related to multiplicative characters

Even though the intended use is when domain and target of the characters
are commutative rings, we define them in the more general setting when
the domain is a commutative monoid and the target is a commutative monoid
with zero. (We need a zero in the target, since non-units are supposed
to map to zero.)

In this setting, there is an equivalence between multiplicative characters
`R â†’ R'` and group homomorphisms `RË£ â†’ R'Ë£`, and the multiplicative characters
have a natural structure as a commutative group.
-/


universe u v

section Defi

-- The domain of our multiplicative characters
variable (R : Type u) [CommMonoid R]

-- The target
variable (R' : Type v) [CommMonoidWithZero R']

/-- Define a structure for multiplicative characters.
A multiplicative character from a commutative monoid `R` to a commutative monoid with zero `R'`
is a homomorphism of (multiplicative) monoids that sends non-units to zero. -/
structure MulChar extends MonoidHom R R' where
  map_nonunit' : âˆ€ a : R, Â¬IsUnit a â†’ toFun a = 0
#align mul_char MulChar

instance funLike : FunLike (MulChar R R') R (fun _ => R') :=
  âŸ¨fun Ï‡ => Ï‡.toFun,
    fun Ï‡â‚€ Ï‡â‚ h => by cases Ï‡â‚€; cases Ï‡â‚; congr; apply MonoidHom.ext (fun _ => congr_fun h _)âŸ©
                      -- âŠ¢ { toMonoidHom := toMonoidHomâœ, map_nonunit' := map_nonunit'âœ } = Ï‡â‚
                                -- âŠ¢ { toMonoidHom := toMonoidHomâœÂ¹, map_nonunit' := map_nonunit'âœÂ¹ } = { toMonoi â€¦
                                          -- âŠ¢ toMonoidHomâœÂ¹ = toMonoidHomâœ
                                                 -- ğŸ‰ no goals

/-- This is the corresponding extension of `MonoidHomClass`. -/
class MulCharClass (F : Type*) (R R' : outParam <| Type*) [CommMonoid R]
  [CommMonoidWithZero R'] extends MonoidHomClass F R R' where
  map_nonunit : âˆ€ (Ï‡ : F) {a : R} (_ : Â¬IsUnit a), Ï‡ a = 0
#align mul_char_class MulCharClass

initialize_simps_projections MulChar (toFun â†’ apply, -toMonoidHom)

attribute [simp] MulCharClass.map_nonunit

end Defi

section Group

namespace MulChar

-- The domain of our multiplicative characters
variable {R : Type u} [CommMonoid R]

-- The target
variable {R' : Type v} [CommMonoidWithZero R']

section trivial

variable (R R')

/-- The trivial multiplicative character. It takes the value `0` on non-units and
the value `1` on units. -/
@[simps]
noncomputable def trivial : MulChar R R' where
  toFun := by classical exact fun x => if IsUnit x then 1 else 0
              -- ğŸ‰ no goals
  map_nonunit' := by
    intro a ha
    -- âŠ¢ OneHom.toFun (â†‘{ toOneHom := { toFun := fun x => if IsUnit x then 1 else 0,  â€¦
    simp only [ha, if_false]
                 -- ğŸ‰ no goals
    -- ğŸ‰ no goals
  map_one' := by simp only [isUnit_one, if_true]
    -- âŠ¢ OneHom.toFun { toFun := fun x => if IsUnit x then 1 else 0, map_one' := (_ : â€¦
  map_mul' := by
    intro x y
    classical
      simp only [IsUnit.mul_iff, boole_mul]
      split_ifs <;> tauto
#align mul_char.trivial MulChar.trivial

end trivial

@[simp]
theorem coe_mk (f : R â†’* R') (hf) : (MulChar.mk f hf : R â†’ R') = f :=
  rfl
#align mul_char.coe_mk MulChar.coe_mk

/-- Extensionality. See `ext` below for the version that will actually be used. -/
theorem ext' {Ï‡ Ï‡' : MulChar R R'} (h : âˆ€ a, Ï‡ a = Ï‡' a) : Ï‡ = Ï‡' := by
  cases Ï‡
  -- âŠ¢ { toMonoidHom := toMonoidHomâœ, map_nonunit' := map_nonunit'âœ } = Ï‡'
  cases Ï‡'
  -- âŠ¢ { toMonoidHom := toMonoidHomâœÂ¹, map_nonunit' := map_nonunit'âœÂ¹ } = { toMonoi â€¦
  congr
  -- âŠ¢ toMonoidHomâœÂ¹ = toMonoidHomâœ
  exact MonoidHom.ext h
  -- ğŸ‰ no goals
#align mul_char.ext' MulChar.ext'

instance : MulCharClass (MulChar R R') R R' where
  coe Ï‡ := Ï‡.toMonoidHom.toFun
  coe_injective' _ _ h := ext' fun a => congr_fun h a
  map_mul Ï‡ := Ï‡.map_mul'
  map_one Ï‡ := Ï‡.map_one'
  map_nonunit Ï‡ := Ï‡.map_nonunit' _

theorem map_nonunit (Ï‡ : MulChar R R') {a : R} (ha : Â¬IsUnit a) : Ï‡ a = 0 :=
  Ï‡.map_nonunit' a ha
#align mul_char.map_nonunit MulChar.map_nonunit

/-- Extensionality. Since `MulChar`s always take the value zero on non-units, it is sufficient
to compare the values on units. -/
@[ext]
theorem ext {Ï‡ Ï‡' : MulChar R R'} (h : âˆ€ a : RË£, Ï‡ a = Ï‡' a) : Ï‡ = Ï‡' := by
  apply ext'
  -- âŠ¢ âˆ€ (a : R), â†‘Ï‡ a = â†‘Ï‡' a
  intro a
  -- âŠ¢ â†‘Ï‡ a = â†‘Ï‡' a
  by_cases ha : IsUnit a
  -- âŠ¢ â†‘Ï‡ a = â†‘Ï‡' a
  Â· exact h ha.unit
    -- ğŸ‰ no goals
  Â· rw [map_nonunit Ï‡ ha, map_nonunit Ï‡' ha]
    -- ğŸ‰ no goals
#align mul_char.ext MulChar.ext

theorem ext_iff {Ï‡ Ï‡' : MulChar R R'} : Ï‡ = Ï‡' â†” âˆ€ a : RË£, Ï‡ a = Ï‡' a :=
  âŸ¨by
    rintro rfl a
    -- âŠ¢ â†‘Ï‡ â†‘a = â†‘Ï‡ â†‘a
    rfl, extâŸ©
    -- ğŸ‰ no goals
#align mul_char.ext_iff MulChar.ext_iff

/-!
### Equivalence of multiplicative characters with homomorphisms on units

We show that restriction / extension by zero gives an equivalence
between `MulChar R R'` and `RË£ â†’* R'Ë£`.
-/


/-- Turn a `MulChar` into a homomorphism between the unit groups. -/
def toUnitHom (Ï‡ : MulChar R R') : RË£ â†’* R'Ë£ :=
  Units.map Ï‡
#align mul_char.to_unit_hom MulChar.toUnitHom

theorem coe_toUnitHom (Ï‡ : MulChar R R') (a : RË£) : â†‘(Ï‡.toUnitHom a) = Ï‡ a :=
  rfl
#align mul_char.coe_to_unit_hom MulChar.coe_toUnitHom

/-- Turn a homomorphism between unit groups into a `MulChar`. -/
noncomputable def ofUnitHom (f : RË£ â†’* R'Ë£) : MulChar R R' where
  toFun := by classical exact fun x => if hx : IsUnit x then f hx.unit else 0
              -- ğŸ‰ no goals
  map_one' := by
    have h1 : (isUnit_one.unit : RË£) = 1 := Units.eq_iff.mp rfl
    -- âŠ¢ (if hx : IsUnit 1 then â†‘(â†‘f (IsUnit.unit hx)) else 0) = 1
    simp only [h1, dif_pos, Units.val_eq_one, map_one, isUnit_one]
    -- ğŸ‰ no goals
  map_mul' := by
    classical
      intro x y
      by_cases hx : IsUnit x
      Â· simp only [hx, IsUnit.mul_iff, true_and_iff, dif_pos]
        by_cases hy : IsUnit y
        Â· simp only [hy, dif_pos]
          have hm : (IsUnit.mul_iff.mpr âŸ¨hx, hyâŸ©).unit = hx.unit * hy.unit := Units.eq_iff.mp rfl
          rw [hm, map_mul]
          norm_cast
        Â· simp only [hy, not_false_iff, dif_neg, mul_zero]
      Â· simp only [hx, IsUnit.mul_iff, false_and_iff, not_false_iff, dif_neg, zero_mul]
  map_nonunit' := by
    intro a ha
    -- âŠ¢ OneHom.toFun (â†‘{ toOneHom := { toFun := fun x => if hx : IsUnit x then â†‘(â†‘f  â€¦
    simp only [ha, not_false_iff, dif_neg]
    -- ğŸ‰ no goals
#align mul_char.of_unit_hom MulChar.ofUnitHom

theorem ofUnitHom_coe (f : RË£ â†’* R'Ë£) (a : RË£) : ofUnitHom f â†‘a = f a := by simp [ofUnitHom]
                                                                            -- ğŸ‰ no goals
#align mul_char.of_unit_hom_coe MulChar.ofUnitHom_coe

/-- The equivalence between multiplicative characters and homomorphisms of unit groups. -/
noncomputable def equivToUnitHom : MulChar R R' â‰ƒ (RË£ â†’* R'Ë£) where
  toFun := toUnitHom
  invFun := ofUnitHom
  left_inv := by
    intro Ï‡
    -- âŠ¢ ofUnitHom (toUnitHom Ï‡) = Ï‡
    ext x
    -- âŠ¢ â†‘(ofUnitHom (toUnitHom Ï‡)) â†‘x = â†‘Ï‡ â†‘x
    rw [ofUnitHom_coe, coe_toUnitHom]
    -- ğŸ‰ no goals
  right_inv := by
    intro f
    -- âŠ¢ toUnitHom (ofUnitHom f) = f
    ext x
    -- âŠ¢ â†‘(â†‘(toUnitHom (ofUnitHom f)) x) = â†‘(â†‘f x)
    simp only [coe_toUnitHom, ofUnitHom_coe]
    -- ğŸ‰ no goals
#align mul_char.equiv_to_unit_hom MulChar.equivToUnitHom

@[simp]
theorem toUnitHom_eq (Ï‡ : MulChar R R') : toUnitHom Ï‡ = equivToUnitHom Ï‡ :=
  rfl
#align mul_char.to_unit_hom_eq MulChar.toUnitHom_eq

@[simp]
theorem ofUnitHom_eq (Ï‡ : RË£ â†’* R'Ë£) : ofUnitHom Ï‡ = equivToUnitHom.symm Ï‡ :=
  rfl
#align mul_char.of_unit_hom_eq MulChar.ofUnitHom_eq

@[simp]
theorem coe_equivToUnitHom (Ï‡ : MulChar R R') (a : RË£) : â†‘(equivToUnitHom Ï‡ a) = Ï‡ a :=
  coe_toUnitHom Ï‡ a
#align mul_char.coe_equiv_to_unit_hom MulChar.coe_equivToUnitHom

@[simp]
theorem equivToUnitHom_symm_coe (f : RË£ â†’* R'Ë£) (a : RË£) : equivToUnitHom.symm f â†‘a = f a :=
  ofUnitHom_coe f a
#align mul_char.equiv_unit_hom_symm_coe MulChar.equivToUnitHom_symm_coe

/-!
### Commutative group structure on multiplicative characters

The multiplicative characters `R â†’ R'` form a commutative group.
-/


protected theorem map_one (Ï‡ : MulChar R R') : Ï‡ (1 : R) = 1 :=
  Ï‡.map_one'
#align mul_char.map_one MulChar.map_one

/-- If the domain has a zero (and is nontrivial), then `Ï‡ 0 = 0`. -/
protected theorem map_zero {R : Type u} [CommMonoidWithZero R] [Nontrivial R] (Ï‡ : MulChar R R') :
    Ï‡ (0 : R) = 0 := by rw [map_nonunit Ï‡ not_isUnit_zero]
                        -- ğŸ‰ no goals
#align mul_char.map_zero MulChar.map_zero

/-- If the domain is a ring `R`, then `Ï‡ (ringChar R) = 0`. -/
theorem map_ringChar {R : Type u} [CommRing R] [Nontrivial R] (Ï‡ : MulChar R R') :
    Ï‡ (ringChar R) = 0 := by rw [ringChar.Nat.cast_ringChar, Ï‡.map_zero]
                             -- ğŸ‰ no goals
#align mul_char.map_ring_char MulChar.map_ringChar

noncomputable instance hasOne : One (MulChar R R') :=
  âŸ¨trivial R R'âŸ©
#align mul_char.has_one MulChar.hasOne

noncomputable instance inhabited : Inhabited (MulChar R R') :=
  âŸ¨1âŸ©
#align mul_char.inhabited MulChar.inhabited

/-- Evaluation of the trivial character -/
@[simp]
theorem one_apply_coe (a : RË£) : (1 : MulChar R R') a = 1 := by classical exact dif_pos a.isUnit
                                                                -- ğŸ‰ no goals
#align mul_char.one_apply_coe MulChar.one_apply_coe

/-- Multiplication of multiplicative characters. (This needs the target to be commutative.) -/
def mul (Ï‡ Ï‡' : MulChar R R') : MulChar R R' :=
  { Ï‡.toMonoidHom * Ï‡'.toMonoidHom with
    toFun := Ï‡ * Ï‡'
    map_nonunit' := fun a ha => by simp only [map_nonunit Ï‡ ha, zero_mul, Pi.mul_apply] }
                                   -- ğŸ‰ no goals
#align mul_char.mul MulChar.mul

instance hasMul : Mul (MulChar R R') :=
  âŸ¨mulâŸ©
#align mul_char.has_mul MulChar.hasMul

theorem mul_apply (Ï‡ Ï‡' : MulChar R R') (a : R) : (Ï‡ * Ï‡') a = Ï‡ a * Ï‡' a :=
  rfl
#align mul_char.mul_apply MulChar.mul_apply

@[simp]
theorem coeToFun_mul (Ï‡ Ï‡' : MulChar R R') : â‡‘(Ï‡ * Ï‡') = Ï‡ * Ï‡' :=
  rfl
#align mul_char.coe_to_fun_mul MulChar.coeToFun_mul

protected theorem one_mul (Ï‡ : MulChar R R') : (1 : MulChar R R') * Ï‡ = Ï‡ := by
  ext
  -- âŠ¢ â†‘(1 * Ï‡) â†‘aâœ = â†‘Ï‡ â†‘aâœ
  simp only [one_mul, Pi.mul_apply, MulChar.coeToFun_mul, MulChar.one_apply_coe]
  -- ğŸ‰ no goals
#align mul_char.one_mul MulChar.one_mul

protected theorem mul_one (Ï‡ : MulChar R R') : Ï‡ * 1 = Ï‡ := by
  ext
  -- âŠ¢ â†‘(Ï‡ * 1) â†‘aâœ = â†‘Ï‡ â†‘aâœ
  simp only [mul_one, Pi.mul_apply, MulChar.coeToFun_mul, MulChar.one_apply_coe]
  -- ğŸ‰ no goals
#align mul_char.mul_one MulChar.mul_one

/-- The inverse of a multiplicative character. We define it as `inverse âˆ˜ Ï‡`. -/
noncomputable def inv (Ï‡ : MulChar R R') : MulChar R R' :=
  { MonoidWithZero.inverse.toMonoidHom.comp Ï‡.toMonoidHom with
    toFun := fun a => MonoidWithZero.inverse (Ï‡ a)
    map_nonunit' := fun a ha => by simp [map_nonunit _ ha] }
                                   -- ğŸ‰ no goals
#align mul_char.inv MulChar.inv

noncomputable instance hasInv : Inv (MulChar R R') :=
  âŸ¨invâŸ©
#align mul_char.has_inv MulChar.hasInv

/-- The inverse of a multiplicative character `Ï‡`, applied to `a`, is the inverse of `Ï‡ a`. -/
theorem inv_apply_eq_inv (Ï‡ : MulChar R R') (a : R) : Ï‡â»Â¹ a = Ring.inverse (Ï‡ a) :=
  Eq.refl <| inv Ï‡ a
#align mul_char.inv_apply_eq_inv MulChar.inv_apply_eq_inv

/-- The inverse of a multiplicative character `Ï‡`, applied to `a`, is the inverse of `Ï‡ a`.
Variant when the target is a field -/
theorem inv_apply_eq_inv' {R' : Type v} [Field R'] (Ï‡ : MulChar R R') (a : R) : Ï‡â»Â¹ a = (Ï‡ a)â»Â¹ :=
  (inv_apply_eq_inv Ï‡ a).trans <| Ring.inverse_eq_inv (Ï‡ a)
#align mul_char.inv_apply_eq_inv' MulChar.inv_apply_eq_inv'

/-- When the domain has a zero, then the inverse of a multiplicative character `Ï‡`,
applied to `a`, is `Ï‡` applied to the inverse of `a`. -/
theorem inv_apply {R : Type u} [CommMonoidWithZero R] (Ï‡ : MulChar R R') (a : R) :
    Ï‡â»Â¹ a = Ï‡ (Ring.inverse a) := by
  by_cases ha : IsUnit a
  -- âŠ¢ â†‘Ï‡â»Â¹ a = â†‘Ï‡ (Ring.inverse a)
  Â· rw [inv_apply_eq_inv]
    -- âŠ¢ Ring.inverse (â†‘Ï‡ a) = â†‘Ï‡ (Ring.inverse a)
    have h := IsUnit.map Ï‡ ha
    -- âŠ¢ Ring.inverse (â†‘Ï‡ a) = â†‘Ï‡ (Ring.inverse a)
    apply_fun (Ï‡ a * Â·) using IsUnit.mul_right_injective h
    -- âŠ¢ (fun x => â†‘Ï‡ a * x) (Ring.inverse (â†‘Ï‡ a)) = (fun x => â†‘Ï‡ a * x) (â†‘Ï‡ (Ring.in â€¦
    dsimp only
    -- âŠ¢ â†‘Ï‡ a * Ring.inverse (â†‘Ï‡ a) = â†‘Ï‡ a * â†‘Ï‡ (Ring.inverse a)
    -- Porting note: was
    -- rw [Ring.mul_inverse_cancel _ h, â† map_mul, Ring.mul_inverse_cancel _ ha, MulChar.map_one]
    erw [Ring.mul_inverse_cancel _ h, â† map_mul, Ring.mul_inverse_cancel _ ha]
    -- âŠ¢ 1 = â†‘Ï‡ 1
    exact (MulChar.map_one Ï‡).symm
    -- ğŸ‰ no goals
  Â· revert ha
    -- âŠ¢ Â¬IsUnit a â†’ â†‘Ï‡â»Â¹ a = â†‘Ï‡ (Ring.inverse a)
    nontriviality R
    -- âŠ¢ Â¬IsUnit a â†’ â†‘Ï‡â»Â¹ a = â†‘Ï‡ (Ring.inverse a)
    intro ha
    -- âŠ¢ â†‘Ï‡â»Â¹ a = â†‘Ï‡ (Ring.inverse a)
    -- `nontriviality R` by itself doesn't do it
    rw [map_nonunit _ ha, Ring.inverse_non_unit a ha, MulChar.map_zero Ï‡]
    -- ğŸ‰ no goals
#align mul_char.inv_apply MulChar.inv_apply

/-- When the domain has a zero, then the inverse of a multiplicative character `Ï‡`,
applied to `a`, is `Ï‡` applied to the inverse of `a`. -/
theorem inv_apply' {R : Type u} [Field R] (Ï‡ : MulChar R R') (a : R) : Ï‡â»Â¹ a = Ï‡ aâ»Â¹ :=
  (inv_apply Ï‡ a).trans <| congr_arg _ (Ring.inverse_eq_inv a)
#align mul_char.inv_apply' MulChar.inv_apply'

/-- The product of a character with its inverse is the trivial character. -/
-- Porting note: @[simp] can prove this (later)
theorem inv_mul (Ï‡ : MulChar R R') : Ï‡â»Â¹ * Ï‡ = 1 := by
  ext x
  -- âŠ¢ â†‘(Ï‡â»Â¹ * Ï‡) â†‘x = â†‘1 â†‘x
  rw [coeToFun_mul, Pi.mul_apply, inv_apply_eq_inv]
  -- âŠ¢ Ring.inverse (â†‘Ï‡ â†‘x) * â†‘Ï‡ â†‘x = â†‘1 â†‘x
  -- Porting note: was
  -- simp only [Ring.inverse_mul_cancel _ (IsUnit.map _ x.isUnit)]
  erw [Ring.inverse_mul_cancel _ (IsUnit.map Ï‡ x.isUnit)]
  -- âŠ¢ 1 = â†‘1 â†‘x
  rw [one_apply_coe]
  -- ğŸ‰ no goals
#align mul_char.inv_mul MulChar.inv_mul

/-- The commutative group structure on `MulChar R R'`. -/
noncomputable instance commGroup : CommGroup (MulChar R R') :=
  { one := 1
    mul := (Â· * Â·)
    inv := Inv.inv
    mul_left_inv := inv_mul
    mul_assoc := by
      intro Ï‡â‚ Ï‡â‚‚ Ï‡â‚ƒ
      -- âŠ¢ Ï‡â‚ * Ï‡â‚‚ * Ï‡â‚ƒ = Ï‡â‚ * (Ï‡â‚‚ * Ï‡â‚ƒ)
      ext a
      -- âŠ¢ â†‘(Ï‡â‚ * Ï‡â‚‚ * Ï‡â‚ƒ) â†‘a = â†‘(Ï‡â‚ * (Ï‡â‚‚ * Ï‡â‚ƒ)) â†‘a
      simp only [mul_assoc, Pi.mul_apply, MulChar.coeToFun_mul]
      -- ğŸ‰ no goals
    mul_comm := by
      intro Ï‡â‚ Ï‡â‚‚
      -- âŠ¢ Ï‡â‚ * Ï‡â‚‚ = Ï‡â‚‚ * Ï‡â‚
      ext a
      -- âŠ¢ â†‘(Ï‡â‚ * Ï‡â‚‚) â†‘a = â†‘(Ï‡â‚‚ * Ï‡â‚) â†‘a
      simp only [mul_comm, Pi.mul_apply, MulChar.coeToFun_mul]
      -- ğŸ‰ no goals
    one_mul := MulChar.one_mul
    mul_one := MulChar.mul_one }
#align mul_char.comm_group MulChar.commGroup

/-- If `a` is a unit and `n : â„•`, then `(Ï‡ ^ n) a = (Ï‡ a) ^ n`. -/
theorem pow_apply_coe (Ï‡ : MulChar R R') (n : â„•) (a : RË£) : (Ï‡ ^ n) a = Ï‡ a ^ n := by
  induction' n with n ih
  -- âŠ¢ â†‘(Ï‡ ^ Nat.zero) â†‘a = â†‘Ï‡ â†‘a ^ Nat.zero
  Â· rw [pow_zero, pow_zero, one_apply_coe]
    -- ğŸ‰ no goals
  Â· rw [pow_succ, pow_succ, mul_apply, ih]
    -- ğŸ‰ no goals
#align mul_char.pow_apply_coe MulChar.pow_apply_coe

/-- If `n` is positive, then `(Ï‡ ^ n) a = (Ï‡ a) ^ n`. -/
theorem pow_apply' (Ï‡ : MulChar R R') {n : â„•} (hn : 0 < n) (a : R) : (Ï‡ ^ n) a = Ï‡ a ^ n := by
  by_cases ha : IsUnit a
  -- âŠ¢ â†‘(Ï‡ ^ n) a = â†‘Ï‡ a ^ n
  Â· exact pow_apply_coe Ï‡ n ha.unit
    -- ğŸ‰ no goals
  Â· rw [map_nonunit (Ï‡ ^ n) ha, map_nonunit Ï‡ ha, zero_pow hn]
    -- ğŸ‰ no goals
#align mul_char.pow_apply' MulChar.pow_apply'

end MulChar

end Group

end DefinitionAndGroup

/-!
### Properties of multiplicative characters

We introduce the properties of being nontrivial or quadratic and prove
some basic facts about them.

We now assume that domain and target are commutative rings.
-/


section Properties

namespace MulChar

universe u v w

variable {R : Type u} [CommRing R] {R' : Type v} [CommRing R'] {R'' : Type w} [CommRing R'']

/-- A multiplicative character is *nontrivial* if it takes a value `â‰  1` on a unit. -/
def IsNontrivial (Ï‡ : MulChar R R') : Prop :=
  âˆƒ a : RË£, Ï‡ a â‰  1
#align mul_char.is_nontrivial MulChar.IsNontrivial

/-- A multiplicative character is nontrivial iff it is not the trivial character. -/
theorem isNontrivial_iff (Ï‡ : MulChar R R') : Ï‡.IsNontrivial â†” Ï‡ â‰  1 := by
  simp only [IsNontrivial, Ne.def, ext_iff, not_forall, one_apply_coe]
  -- ğŸ‰ no goals
#align mul_char.is_nontrivial_iff MulChar.isNontrivial_iff

/-- A multiplicative character is *quadratic* if it takes only the values `0`, `1`, `-1`. -/
def IsQuadratic (Ï‡ : MulChar R R') : Prop :=
  âˆ€ a, Ï‡ a = 0 âˆ¨ Ï‡ a = 1 âˆ¨ Ï‡ a = -1
#align mul_char.is_quadratic MulChar.IsQuadratic

/-- If two values of quadratic characters with target `â„¤` agree after coercion into a ring
of characteristic not `2`, then they agree in `â„¤`. -/
theorem IsQuadratic.eq_of_eq_coe {Ï‡ : MulChar R â„¤} (hÏ‡ : IsQuadratic Ï‡) {Ï‡' : MulChar R' â„¤}
    (hÏ‡' : IsQuadratic Ï‡') [Nontrivial R''] (hR'' : ringChar R'' â‰  2) {a : R} {a' : R'}
    (h : (Ï‡ a : R'') = Ï‡' a') : Ï‡ a = Ï‡' a' :=
  Int.cast_injOn_of_ringChar_ne_two hR'' (hÏ‡ a) (hÏ‡' a') h
#align mul_char.is_quadratic.eq_of_eq_coe MulChar.IsQuadratic.eq_of_eq_coe

/-- We can post-compose a multiplicative character with a ring homomorphism. -/
@[simps]
def ringHomComp (Ï‡ : MulChar R R') (f : R' â†’+* R'') : MulChar R R'' :=
  { f.toMonoidHom.comp Ï‡.toMonoidHom with
    toFun := fun a => f (Ï‡ a)
    map_nonunit' := fun a ha => by simp only [map_nonunit Ï‡ ha, map_zero] }
                                   -- ğŸ‰ no goals
#align mul_char.ring_hom_comp MulChar.ringHomComp

/-- Composition with an injective ring homomorphism preserves nontriviality. -/
theorem IsNontrivial.comp {Ï‡ : MulChar R R'} (hÏ‡ : Ï‡.IsNontrivial) {f : R' â†’+* R''}
    (hf : Function.Injective f) : (Ï‡.ringHomComp f).IsNontrivial := by
  obtain âŸ¨a, haâŸ© := hÏ‡
  -- âŠ¢ IsNontrivial (ringHomComp Ï‡ f)
  use a
  -- âŠ¢ â†‘(ringHomComp Ï‡ f) â†‘a â‰  1
  simp_rw [ringHomComp_apply, â† RingHom.map_one f]
  -- âŠ¢ â†‘f (â†‘Ï‡ â†‘a) â‰  â†‘f 1
  exact fun h => ha (hf h)
  -- ğŸ‰ no goals
#align mul_char.is_nontrivial.comp MulChar.IsNontrivial.comp

/-- Composition with a ring homomorphism preserves the property of being a quadratic character. -/
theorem IsQuadratic.comp {Ï‡ : MulChar R R'} (hÏ‡ : Ï‡.IsQuadratic) (f : R' â†’+* R'') :
    (Ï‡.ringHomComp f).IsQuadratic := by
  intro a
  -- âŠ¢ â†‘(ringHomComp Ï‡ f) a = 0 âˆ¨ â†‘(ringHomComp Ï‡ f) a = 1 âˆ¨ â†‘(ringHomComp Ï‡ f) a = â€¦
  rcases hÏ‡ a with (ha | ha | ha) <;> simp [ha]
                                      -- ğŸ‰ no goals
                                      -- ğŸ‰ no goals
                                      -- ğŸ‰ no goals
#align mul_char.is_quadratic.comp MulChar.IsQuadratic.comp

/-- The inverse of a quadratic character is itself. â†’  -/
theorem IsQuadratic.inv {Ï‡ : MulChar R R'} (hÏ‡ : Ï‡.IsQuadratic) : Ï‡â»Â¹ = Ï‡ := by
  ext x
  -- âŠ¢ â†‘Ï‡â»Â¹ â†‘x = â†‘Ï‡ â†‘x
  rw [inv_apply_eq_inv]
  -- âŠ¢ Ring.inverse (â†‘Ï‡ â†‘x) = â†‘Ï‡ â†‘x
  rcases hÏ‡ x with (hâ‚€ | hâ‚ | hâ‚‚)
  Â· rw [hâ‚€, Ring.inverse_zero]
    -- ğŸ‰ no goals
  Â· rw [hâ‚, Ring.inverse_one]
    -- ğŸ‰ no goals
  Â· -- Porting note: was `by norm_cast`
    have : (-1 : R') = (-1 : R'Ë£) := by rw [Units.val_neg, Units.val_one]
    -- âŠ¢ Ring.inverse (â†‘Ï‡ â†‘x) = â†‘Ï‡ â†‘x
    rw [hâ‚‚, this, Ring.inverse_unit (-1 : R'Ë£)]
    -- âŠ¢ â†‘(-1)â»Â¹ = â†‘(-1)
    rfl
    -- ğŸ‰ no goals
#align mul_char.is_quadratic.inv MulChar.IsQuadratic.inv

/-- The square of a quadratic character is the trivial character. -/
theorem IsQuadratic.sq_eq_one {Ï‡ : MulChar R R'} (hÏ‡ : Ï‡.IsQuadratic) : Ï‡ ^ 2 = 1 := by
  rw [â† mul_left_inv Ï‡, pow_two, hÏ‡.inv]
  -- ğŸ‰ no goals
#align mul_char.is_quadratic.sq_eq_one MulChar.IsQuadratic.sq_eq_one

/-- The `p`th power of a quadratic character is itself, when `p` is the (prime) characteristic
of the target ring. -/
theorem IsQuadratic.pow_char {Ï‡ : MulChar R R'} (hÏ‡ : Ï‡.IsQuadratic) (p : â„•) [hp : Fact p.Prime]
    [CharP R' p] : Ï‡ ^ p = Ï‡ := by
  ext x
  -- âŠ¢ â†‘(Ï‡ ^ p) â†‘x = â†‘Ï‡ â†‘x
  rw [pow_apply_coe]
  -- âŠ¢ â†‘Ï‡ â†‘x ^ p = â†‘Ï‡ â†‘x
  rcases hÏ‡ x with (hx | hx | hx) <;> rw [hx]
                                      -- âŠ¢ 0 ^ p = 0
                                      -- âŠ¢ 1 ^ p = 1
                                      -- âŠ¢ (-1) ^ p = -1
  Â· rw [zero_pow (@Fact.out p.Prime).pos]
    -- ğŸ‰ no goals
  Â· rw [one_pow]
    -- ğŸ‰ no goals
  Â· exact CharP.neg_one_pow_char R' p
    -- ğŸ‰ no goals
#align mul_char.is_quadratic.pow_char MulChar.IsQuadratic.pow_char

/-- The `n`th power of a quadratic character is the trivial character, when `n` is even. -/
theorem IsQuadratic.pow_even {Ï‡ : MulChar R R'} (hÏ‡ : Ï‡.IsQuadratic) {n : â„•} (hn : Even n) :
    Ï‡ ^ n = 1 := by
  obtain âŸ¨n, rflâŸ© := even_iff_two_dvd.mp hn
  -- âŠ¢ Ï‡ ^ (2 * n) = 1
  rw [pow_mul, hÏ‡.sq_eq_one, one_pow]
  -- ğŸ‰ no goals
#align mul_char.is_quadratic.pow_even MulChar.IsQuadratic.pow_even

/-- The `n`th power of a quadratic character is itself, when `n` is odd. -/
theorem IsQuadratic.pow_odd {Ï‡ : MulChar R R'} (hÏ‡ : Ï‡.IsQuadratic) {n : â„•} (hn : Odd n) :
    Ï‡ ^ n = Ï‡ := by
  obtain âŸ¨n, rflâŸ© := hn
  -- âŠ¢ Ï‡ ^ (2 * n + 1) = Ï‡
  rw [pow_add, pow_one, hÏ‡.pow_even (even_two_mul _), one_mul]
  -- ğŸ‰ no goals
#align mul_char.is_quadratic.pow_odd MulChar.IsQuadratic.pow_odd

open BigOperators

/-- The sum over all values of a nontrivial multiplicative character on a finite ring is zero
(when the target is a domain). -/
theorem IsNontrivial.sum_eq_zero [Fintype R] [IsDomain R'] {Ï‡ : MulChar R R'}
    (hÏ‡ : Ï‡.IsNontrivial) : âˆ‘ a, Ï‡ a = 0 := by
  rcases hÏ‡ with âŸ¨b, hbâŸ©
  -- âŠ¢ âˆ‘ a : R, â†‘Ï‡ a = 0
  refine' eq_zero_of_mul_eq_self_left hb _
  -- âŠ¢ â†‘Ï‡ â†‘b * âˆ‘ a : R, â†‘Ï‡ a = âˆ‘ a : R, â†‘Ï‡ a
  -- POrting note: `map_mul` isn't applied
  simp only [Finset.mul_sum, â† map_mul]
  -- âŠ¢ âˆ‘ x : R, â†‘Ï‡ (â†‘b * x) = âˆ‘ x : R, â†‘Ï‡ x
  refine Fintype.sum_bijective _ (Units.mulLeft_bijective b) _ _ fun x => rfl
  -- ğŸ‰ no goals
#align mul_char.is_nontrivial.sum_eq_zero MulChar.IsNontrivial.sum_eq_zero

/-- The sum over all values of the trivial multiplicative character on a finite ring is
the cardinality of its unit group. -/
theorem sum_one_eq_card_units [Fintype R] [DecidableEq R] :
    (âˆ‘ a, (1 : MulChar R R') a) = Fintype.card RË£ := by
  calc
    (âˆ‘ a, (1 : MulChar R R') a) = âˆ‘ a : R, if IsUnit a then 1 else 0 :=
      Finset.sum_congr rfl fun a _ => ?_
    _ = ((Finset.univ : Finset R).filter IsUnit).card := Finset.sum_boole
    _ = (Finset.univ.map âŸ¨((â†‘) : RË£ â†’ R), Units.extâŸ©).card := ?_
    _ = Fintype.card RË£ := congr_arg _ (Finset.card_map _)
  Â· split_ifs with h
    -- âŠ¢ â†‘1 a = 1
    Â· exact one_apply_coe h.unit
      -- ğŸ‰ no goals
    Â· exact map_nonunit _ h
      -- ğŸ‰ no goals
  Â· congr
    -- âŠ¢ Finset.filter IsUnit Finset.univ = Finset.map { toFun := Units.val, inj' :=  â€¦
    ext a
    -- âŠ¢ a âˆˆ Finset.filter IsUnit Finset.univ â†” a âˆˆ Finset.map { toFun := Units.val,  â€¦
    simp only [Finset.mem_filter, Finset.mem_univ, true_and_iff, Finset.mem_map,
      Function.Embedding.coeFn_mk, exists_true_left, IsUnit]
#align mul_char.sum_one_eq_card_units MulChar.sum_one_eq_card_units

end MulChar

end Properties
