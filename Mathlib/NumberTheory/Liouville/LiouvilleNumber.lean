/-
Copyright (c) 2020 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Damiano Testa, Jujian Zhang
-/
import Mathlib.NumberTheory.Liouville.Basic

#align_import number_theory.liouville.liouville_number from "leanprover-community/mathlib"@"04e80bb7e8510958cd9aacd32fe2dc147af0b9f1"

/-!

# Liouville constants

This file contains a construction of a family of Liouville numbers, indexed by a natural number $m$.
The most important property is that they are examples of transcendental real numbers.
This fact is recorded in `transcendental_liouvilleNumber`.

More precisely, for a real number $m$, Liouville's constant is
$$
\sum_{i=0}^\infty\frac{1}{m^{i!}}.
$$
The series converges only for $1 < m$. However, there is no restriction on $m$, since,
if the series does not converge, then the sum of the series is defined to be zero.

We prove that, for $m \in \mathbb{N}$ satisfying $2 \le m$, Liouville's constant associated to $m$
is a transcendental number. Classically, the Liouville number for $m = 2$ is the one called
``Liouville's constant''.

## Implementation notes

The indexing $m$ is eventually a natural number satisfying $2 ‚â§ m$. However, we prove the first few
lemmas for $m \in \mathbb{R}$.
-/


noncomputable section

open scoped Nat BigOperators

open Real Finset

/-- For a real number `m`, Liouville's constant is
$$
\sum_{i=0}^\infty\frac{1}{m^{i!}}.
$$
The series converges only for `1 < m`. However, there is no restriction on `m`, since,
if the series does not converge, then the sum of the series is defined to be zero.
-/
def liouvilleNumber (m : ‚Ñù) : ‚Ñù :=
  ‚àë' i : ‚Ñï, 1 / m ^ i !
#align liouville_number liouvilleNumber

namespace LiouvilleNumber

/-- `LiouvilleNumber.partialSum` is the sum of the first `k + 1` terms of Liouville's constant,
i.e.
$$
\sum_{i=0}^k\frac{1}{m^{i!}}.
$$
-/
def partialSum (m : ‚Ñù) (k : ‚Ñï) : ‚Ñù :=
  ‚àë i in range (k + 1), 1 / m ^ i !
#align liouville_number.partial_sum LiouvilleNumber.partialSum

/-- `LiouvilleNumber.remainder` is the sum of the series of the terms in `liouvilleNumber m`
starting from `k+1`, i.e
$$
\sum_{i=k+1}^\infty\frac{1}{m^{i!}}.
$$
-/
def remainder (m : ‚Ñù) (k : ‚Ñï) : ‚Ñù :=
  ‚àë' i, 1 / m ^ (i + (k + 1))!
#align liouville_number.remainder LiouvilleNumber.remainder

/-!
We start with simple observations.
-/


protected theorem summable {m : ‚Ñù} (hm : 1 < m) : Summable fun i : ‚Ñï => 1 / m ^ i ! :=
  summable_one_div_pow_of_le hm Nat.self_le_factorial
#align liouville_number.summable LiouvilleNumber.summable

theorem remainder_summable {m : ‚Ñù} (hm : 1 < m) (k : ‚Ñï) :
    Summable fun i : ‚Ñï => 1 / m ^ (i + (k + 1))! := by
  convert(summable_nat_add_iff (k + 1)).2 (LiouvilleNumber.summable hm)
  -- üéâ no goals
#align liouville_number.remainder_summable LiouvilleNumber.remainder_summable

theorem remainder_pos {m : ‚Ñù} (hm : 1 < m) (k : ‚Ñï) : 0 < remainder m k :=
  tsum_pos (remainder_summable hm k) (fun _ => by positivity) 0 (by positivity)
                                                  -- üéâ no goals
                                                                    -- üéâ no goals
#align liouville_number.remainder_pos LiouvilleNumber.remainder_pos

theorem partialSum_succ (m : ‚Ñù) (n : ‚Ñï) :
    partialSum m (n + 1) = partialSum m n + 1 / m ^ (n + 1)! :=
  sum_range_succ _ _
#align liouville_number.partial_sum_succ LiouvilleNumber.partialSum_succ

/-- Split the sum defining a Liouville number into the first `k` terms and the rest. -/
theorem partialSum_add_remainder {m : ‚Ñù} (hm : 1 < m) (k : ‚Ñï) :
    partialSum m k + remainder m k = liouvilleNumber m :=
  sum_add_tsum_nat_add _ (LiouvilleNumber.summable hm)
#align liouville_number.partial_sum_add_remainder LiouvilleNumber.partialSum_add_remainder

/-! We now prove two useful inequalities, before collecting everything together. -/


/-- An upper estimate on the remainder. This estimate works with `m ‚àà ‚Ñù` satisfying `1 < m` and is
stronger than the estimate `LiouvilleNumber.remainder_lt` below. However, the latter estimate is
more useful for the proof. -/
theorem remainder_lt' (n : ‚Ñï) {m : ‚Ñù} (m1 : 1 < m) :
    remainder m n < (1 - 1 / m)‚Åª¬π * (1 / m ^ (n + 1)!) :=
  -- two useful inequalities
  have m0 : 0 < m := zero_lt_one.trans m1
  have mi : 1 / m < 1 := (div_lt_one m0).mpr m1
  -- to show the strict inequality between these series, we prove that:
  calc
    (‚àë' i, 1 / m ^ (i + (n + 1))!) < ‚àë' i, 1 / m ^ (i + (n + 1)!) :=
        -- 1. the second series dominates the first
        tsum_lt_tsum (fun b => one_div_pow_le_one_div_pow_of_le m1.le
          (b.add_factorial_succ_le_factorial_add_succ n))
        -- 2. the term with index `i = 2` of the first series is strictly smaller than
        -- the corresponding term of the second series
        (one_div_pow_strictAnti m1 (n.add_factorial_succ_lt_factorial_add_succ (i := 2) le_rfl))
        -- 3. the first series is summable
        (remainder_summable m1 n)
        -- 4. the second series is summable, since its terms grow quickly
        (summable_one_div_pow_of_le m1 fun j => le_self_add)
    -- split the sum in the exponent and massage
    _ = ‚àë' i : ‚Ñï, (1 / m) ^ i * (1 / m ^ (n + 1)!) :=
    by simp only [pow_add, one_div, mul_inv, inv_pow]
       -- üéâ no goals
    -- factor the constant `(1 / m ^ (n + 1)!)` out of the series
    _ = (‚àë' i, (1 / m) ^ i) * (1 / m ^ (n + 1)!) := tsum_mul_right
    -- the series is the geometric series
    _ = (1 - 1 / m)‚Åª¬π * (1 / m ^ (n + 1)!) := by rw [tsum_geometric_of_lt_1 (by positivity) mi]
                                                 -- üéâ no goals
#align liouville_number.remainder_lt' LiouvilleNumber.remainder_lt'

theorem aux_calc (n : ‚Ñï) {m : ‚Ñù} (hm : 2 ‚â§ m) :
    (1 - 1 / m)‚Åª¬π * (1 / m ^ (n + 1)!) ‚â§ 1 / (m ^ n !) ^ n :=
  calc
    (1 - 1 / m)‚Åª¬π * (1 / m ^ (n + 1)!) ‚â§ 2 * (1 / m ^ (n + 1)!) :=
      -- the second factors coincide (and are non-negative),
      -- the first factors satisfy the inequality `sub_one_div_inv_le_two`
      mul_le_mul_of_nonneg_right (sub_one_div_inv_le_two hm) (by positivity)
                                                                 -- üéâ no goals
    _ = 2 / m ^ (n + 1)! := (mul_one_div 2 _)
    _ = 2 / m ^ (n ! * (n + 1)) := (congr_arg ((¬∑ / ¬∑) 2) (congr_arg (Pow.pow m) (mul_comm _ _)))
    _ ‚â§ 1 / m ^ (n ! * n) := by
      -- [NB: in this block, I do not follow the brace convention for subgoals -- I wait until
      -- I solve all extraneous goals at once with `exact pow_pos (zero_lt_two.trans_le hm) _`.]
      -- Clear denominators and massage*
      apply (div_le_div_iff _ _).mpr
      conv_rhs => rw [one_mul, mul_add, pow_add, mul_one, pow_mul, mul_comm, ‚Üê pow_mul]
      -- the second factors coincide, so we prove the inequality of the first factors*
      refine' (mul_le_mul_right _).mpr _
      -- solve all the inequalities `0 < m ^ ??`
      any_goals exact pow_pos (zero_lt_two.trans_le hm) _
      -- ‚ä¢ 2 ‚â§ m ^ n !
      -- `2 ‚â§ m ^ n!` is a consequence of monotonicity of exponentiation at `2 ‚â§ m`.
      exact _root_.trans (_root_.trans hm (pow_one _).symm.le)
        (pow_mono (one_le_two.trans hm) n.factorial_pos)
    _ = 1 / (m ^ n !) ^ n := congr_arg ((¬∑ / ¬∑) 1) (pow_mul m n ! n)
#align liouville_number.aux_calc LiouvilleNumber.aux_calc

/-- An upper estimate on the remainder. This estimate works with `m ‚àà ‚Ñù` satisfying `2 ‚â§ m` and is
weaker than the estimate `LiouvilleNumber.remainder_lt'` above. However, this estimate is
more useful for the proof. -/
theorem remainder_lt (n : ‚Ñï) {m : ‚Ñù} (m2 : 2 ‚â§ m) : remainder m n < 1 / (m ^ n !) ^ n :=
  (remainder_lt' n <| one_lt_two.trans_le m2).trans_le (aux_calc _ m2)
#align liouville_number.remainder_lt LiouvilleNumber.remainder_lt

/-! Starting from here, we specialize to the case in which `m` is a natural number. -/


/-- The sum of the `k` initial terms of the Liouville number to base `m` is a ratio of natural
numbers where the denominator is `m ^ k!`. -/
theorem partialSum_eq_rat {m : ‚Ñï} (hm : 0 < m) (k : ‚Ñï) :
    ‚àÉ p : ‚Ñï, partialSum m k = p / (m ^ k ! : ‚Ñù) := by
  induction' k with k h
  -- ‚ä¢ ‚àÉ p, partialSum (‚Üëm) Nat.zero = ‚Üëp / ‚Üë(m ^ Nat.zero !)
  ¬∑ exact ‚ü®1, by rw [partialSum, range_one, sum_singleton, Nat.cast_one, Nat.factorial,
      pow_one, pow_one]‚ü©
  ¬∑ rcases h with ‚ü®p_k, h_k‚ü©
    -- ‚ä¢ ‚àÉ p, partialSum (‚Üëm) (Nat.succ k) = ‚Üëp / ‚Üë(m ^ (Nat.succ k)!)
    use p_k * m ^ ((k + 1)! - k !) + 1
    -- ‚ä¢ partialSum (‚Üëm) (Nat.succ k) = ‚Üë(p_k * m ^ ((k + 1)! - k !) + 1) / ‚Üë(m ^ (Na ‚Ä¶
    rw [partialSum_succ, h_k, div_add_div, div_eq_div_iff, add_mul]
    ¬∑ norm_cast
      -- ‚ä¢ p_k * m ^ (k + 1)! * m ^ (Nat.succ k)! + m ^ k ! * 1 * m ^ (Nat.succ k)! = ( ‚Ä¶
      rw [add_mul, one_mul, Nat.factorial_succ, add_mul, one_mul, add_tsub_cancel_right, pow_add]
      -- ‚ä¢ p_k * (m ^ (k * k !) * m ^ k !) * (m ^ (k * k !) * m ^ k !) + m ^ k ! * 1 *  ‚Ä¶
      simp [mul_assoc]
      -- üéâ no goals
    all_goals positivity
    -- üéâ no goals
#align liouville_number.partial_sum_eq_rat LiouvilleNumber.partialSum_eq_rat

end LiouvilleNumber

open LiouvilleNumber

theorem liouville_liouvilleNumber {m : ‚Ñï} (hm : 2 ‚â§ m) : Liouville (liouvilleNumber m) := by
  -- two useful inequalities
  have mZ1 : 1 < (m : ‚Ñ§) := by norm_cast
  -- ‚ä¢ Liouville (liouvilleNumber ‚Üëm)
  have m1 : 1 < (m : ‚Ñù) := by norm_cast
  -- ‚ä¢ Liouville (liouvilleNumber ‚Üëm)
  intro n
  -- ‚ä¢ ‚àÉ a b, 1 < b ‚àß liouvilleNumber ‚Üëm ‚â† ‚Üëa / ‚Üëb ‚àß |liouvilleNumber ‚Üëm - ‚Üëa / ‚Üëb| ‚Ä¶
  -- the first `n` terms sum to `p / m ^ k!`
  rcases partialSum_eq_rat (zero_lt_two.trans_le hm) n with ‚ü®p, hp‚ü©
  -- ‚ä¢ ‚àÉ a b, 1 < b ‚àß liouvilleNumber ‚Üëm ‚â† ‚Üëa / ‚Üëb ‚àß |liouvilleNumber ‚Üëm - ‚Üëa / ‚Üëb| ‚Ä¶
  refine' ‚ü®p, m ^ n !, by rw [Nat.cast_pow]; exact one_lt_pow mZ1 n.factorial_ne_zero, _‚ü©
  -- ‚ä¢ liouvilleNumber ‚Üëm ‚â† ‚Üë‚Üëp / ‚Üë‚Üë(m ^ n !) ‚àß |liouvilleNumber ‚Üëm - ‚Üë‚Üëp / ‚Üë‚Üë(m ^  ‚Ä¶
  push_cast
  -- ‚ä¢ liouvilleNumber ‚Üëm ‚â† ‚Üëp / ‚Üëm ^ n ! ‚àß |liouvilleNumber ‚Üëm - ‚Üëp / ‚Üëm ^ n !| <  ‚Ä¶
  rw [Nat.cast_pow] at hp
  -- ‚ä¢ liouvilleNumber ‚Üëm ‚â† ‚Üëp / ‚Üëm ^ n ! ‚àß |liouvilleNumber ‚Üëm - ‚Üëp / ‚Üëm ^ n !| <  ‚Ä¶
  -- separate out the sum of the first `n` terms and the rest
  rw [‚Üê partialSum_add_remainder m1 n, ‚Üê hp]
  -- ‚ä¢ partialSum (‚Üëm) n + remainder (‚Üëm) n ‚â† partialSum (‚Üëm) n ‚àß |partialSum (‚Üëm)  ‚Ä¶
  have hpos := remainder_pos m1 n
  -- ‚ä¢ partialSum (‚Üëm) n + remainder (‚Üëm) n ‚â† partialSum (‚Üëm) n ‚àß |partialSum (‚Üëm)  ‚Ä¶
  simpa [abs_of_pos hpos, hpos.ne'] using @remainder_lt n m (by assumption_mod_cast)
  -- üéâ no goals
#align liouville_liouville_number liouville_liouvilleNumber

theorem transcendental_liouvilleNumber {m : ‚Ñï} (hm : 2 ‚â§ m) :
    Transcendental ‚Ñ§ (liouvilleNumber m) :=
  (liouville_liouvilleNumber hm).transcendental
#align transcendental_liouville_number transcendental_liouvilleNumber
