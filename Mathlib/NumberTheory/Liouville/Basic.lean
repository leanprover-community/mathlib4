/-
Copyright (c) 2020 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Damiano Testa, Jujian Zhang
-/
import Mathlib.Analysis.Calculus.MeanValue
import Mathlib.Analysis.Calculus.Deriv.Polynomial
import Mathlib.Data.Polynomial.DenomsClearable
import Mathlib.Data.Real.Irrational
import Mathlib.Topology.Algebra.Polynomial

#align_import number_theory.liouville.basic from "leanprover-community/mathlib"@"04e80bb7e8510958cd9aacd32fe2dc147af0b9f1"

/-!

# Liouville's theorem

This file contains a proof of Liouville's theorem stating that all Liouville numbers are
transcendental.

To obtain this result, there is first a proof that Liouville numbers are irrational and two
technical lemmas.  These lemmas exploit the fact that a polynomial with integer coefficients
takes integer values at integers.  When evaluating at a rational number, we can clear denominators
and obtain precise inequalities that ultimately allow us to prove transcendence of
Liouville numbers.
-/


/-- A Liouville number is a real number `x` such that for every natural number `n`, there exist
`a, b ‚àà ‚Ñ§` with `1 < b` such that `0 < |x - a/b| < 1/b‚Åø`.
In the implementation, the condition `x ‚â† a/b` replaces the traditional equivalent `0 < |x - a/b|`.
-/
def Liouville (x : ‚Ñù) :=
  ‚àÄ n : ‚Ñï, ‚àÉ a b : ‚Ñ§, 1 < b ‚àß x ‚â† a / b ‚àß |x - a / b| < 1 / (b : ‚Ñù) ^ n
#align liouville Liouville

namespace Liouville

protected theorem irrational {x : ‚Ñù} (h : Liouville x) : Irrational x := by
  -- By contradiction, `x = a / b`, with `a ‚àà ‚Ñ§`, `0 < b ‚àà ‚Ñï` is a Liouville number,
  rintro ‚ü®‚ü®a, b, bN0, cop‚ü©, rfl‚ü©
  -- ‚ä¢ False
  -- clear up the mess of constructions of rationals
  rw [Rat.cast_mk', ‚Üê div_eq_mul_inv] at h
  -- ‚ä¢ False
  -- Since `a / b` is a Liouville number, there are `p, q ‚àà ‚Ñ§`, with `q1 : 1 < q`,
  -- `a0 : a / b ‚â† p / q` and `a1 : |a / b - p / q| < 1 / q ^ (b + 1)`
  rcases h (b + 1) with ‚ü®p, q, q1, a0, a1‚ü©
  -- ‚ä¢ False
  -- A few useful inequalities
  have qR0 : (0 : ‚Ñù) < q := Int.cast_pos.mpr (zero_lt_one.trans q1)
  -- ‚ä¢ False
  have b0 : (b : ‚Ñù) ‚â† 0 := Nat.cast_ne_zero.mpr bN0
  -- ‚ä¢ False
  have bq0 : (0 : ‚Ñù) < b * q := mul_pos (Nat.cast_pos.mpr bN0.bot_lt) qR0
  -- ‚ä¢ False
  -- At a1, clear denominators...
  replace a1 : |a * q - b * p| * q ^ (b + 1) < b * q
  -- ‚ä¢ |a * q - ‚Üëb * p| * q ^ (b + 1) < ‚Üëb * q
  ¬∑ rw [div_sub_div _ _ b0 qR0.ne', abs_div, div_lt_div_iff (abs_pos.mpr bq0.ne') (pow_pos qR0 _),
      abs_of_pos bq0, one_mul] at a1
    exact_mod_cast a1
    -- üéâ no goals
  -- At a0, clear denominators...
  replace a0 : a * q - ‚Üëb * p ‚â† 0;
  -- ‚ä¢ a * q - ‚Üëb * p ‚â† 0
  ¬∑ rw [Ne.def, div_eq_div_iff b0 qR0.ne', mul_comm (p : ‚Ñù), ‚Üê sub_eq_zero] at a0
    -- ‚ä¢ a * q - ‚Üëb * p ‚â† 0
    exact_mod_cast a0
    -- üéâ no goals
  -- Actually, `q` is a natural number
  lift q to ‚Ñï using (zero_lt_one.trans q1).le
  -- ‚ä¢ False
  -- Looks innocuous, but we now have an integer with non-zero absolute value: this is at
  -- least one away from zero.  The gain here is what gets the proof going.
  have ap : 0 < |a * ‚Üëq - ‚Üëb * p| := abs_pos.mpr a0
  -- ‚ä¢ False
  -- Actually, the absolute value of an integer is a natural number
  lift |a * ‚Üëq - ‚Üëb * p| to ‚Ñï using abs_nonneg (a * ‚Üëq - ‚Üëb * p) with e he
  -- ‚ä¢ False
  -- At a1, revert to natural numbers
  rw [‚Üê Int.ofNat_mul, ‚Üê Int.coe_nat_pow, ‚Üê Int.ofNat_mul, Int.ofNat_lt] at a1
  -- ‚ä¢ False
  -- Recall this is by contradiction: we obtained the inequality `b * q ‚â§ x * q ^ (b + 1)`, so
  -- we are done.
  exact not_le.mpr a1 (Nat.mul_lt_mul_pow_succ (Int.coe_nat_pos.mp ap) (Int.ofNat_lt.mp q1)).le
  -- üéâ no goals
#align liouville.irrational Liouville.irrational

open Polynomial Metric Set Real RingHom

open scoped Polynomial

/-- Let `Z, N` be types, let `R` be a metric space, let `Œ± : R` be a point and let
`j : Z ‚Üí N ‚Üí R` be a function.  We aim to estimate how close we can get to `Œ±`, while staying
in the image of `j`.  The points `j z a` of `R` in the image of `j` come with a "cost" equal to
`d a`.  As we get closer to `Œ±` while staying in the image of `j`, we are interested in bounding
the quantity `d a * dist Œ± (j z a)` from below by a strictly positive amount `1 / A`: the intuition
is that approximating well `Œ±` with the points in the image of `j` should come at a high cost.  The
hypotheses on the function `f : R ‚Üí R` provide us with sufficient conditions to ensure our goal.
The first hypothesis is that `f` is Lipschitz at `Œ±`: this yields a bound on the distance.
The second hypothesis is specific to the Liouville argument and provides the missing bound
involving the cost function `d`.

This lemma collects the properties used in the proof of `exists_pos_real_of_irrational_root`.
It is stated in more general form than needed: in the intended application, `Z = ‚Ñ§`, `N = ‚Ñï`,
`R = ‚Ñù`, `d a = (a + 1) ^ f.nat_degree`, `j z a = z / (a + 1)`, `f ‚àà ‚Ñ§[x]`, `Œ±` is an irrational
root of `f`, `Œµ` is small, `M` is a bound on the Lipschitz constant of `f` near `Œ±`, `n` is
the degree of the polynomial `f`.
-/
theorem exists_one_le_pow_mul_dist {Z N R : Type*} [PseudoMetricSpace R] {d : N ‚Üí ‚Ñù}
    {j : Z ‚Üí N ‚Üí R} {f : R ‚Üí R} {Œ± : R} {Œµ M : ‚Ñù}
    -- denominators are positive
    (d0 : ‚àÄ a : N, 1 ‚â§ d a)
    (e0 : 0 < Œµ)
    -- function is Lipschitz at Œ±
    (B : ‚àÄ ‚¶Éy : R‚¶Ñ, y ‚àà closedBall Œ± Œµ ‚Üí dist (f Œ±) (f y) ‚â§ dist Œ± y * M)
    -- clear denominators
    (L : ‚àÄ ‚¶Éz : Z‚¶Ñ, ‚àÄ ‚¶Éa : N‚¶Ñ, j z a ‚àà closedBall Œ± Œµ ‚Üí 1 ‚â§ d a * dist (f Œ±) (f (j z a))) :
    ‚àÉ A : ‚Ñù, 0 < A ‚àß ‚àÄ z : Z, ‚àÄ a : N, 1 ‚â§ d a * (dist Œ± (j z a) * A) := by
  -- A useful inequality to keep at hand
  have me0 : 0 < max (1 / Œµ) M := lt_max_iff.mpr (Or.inl (one_div_pos.mpr e0))
  -- ‚ä¢ ‚àÉ A, 0 < A ‚àß ‚àÄ (z : Z) (a : N), 1 ‚â§ d a * (dist Œ± (j z a) * A)
  -- The maximum between `1 / Œµ` and `M` works
  refine' ‚ü®max (1 / Œµ) M, me0, fun z a => _‚ü©
  -- ‚ä¢ 1 ‚â§ d a * (dist Œ± (j z a) * max (1 / Œµ) M)
  -- First, let's deal with the easy case in which we are far away from `Œ±`
  by_cases dm1 : 1 ‚â§ dist Œ± (j z a) * max (1 / Œµ) M
  -- ‚ä¢ 1 ‚â§ d a * (dist Œ± (j z a) * max (1 / Œµ) M)
  ¬∑ exact one_le_mul_of_one_le_of_one_le (d0 a) dm1
    -- üéâ no goals
  ¬∑ -- `j z a = z / (a + 1)`: we prove that this ratio is close to `Œ±`
    have : j z a ‚àà closedBall Œ± Œµ := by
      refine' mem_closedBall'.mp (le_trans _ ((one_div_le me0 e0).mpr (le_max_left _ _)))
      exact (le_div_iff me0).mpr (not_le.mp dm1).le
    -- use the "separation from `1`" (assumption `L`) for numerators,
    refine' (L this).trans _
    -- ‚ä¢ d a * dist (f Œ±) (f (j z a)) ‚â§ d a * (dist Œ± (j z a) * max (1 / Œµ) M)
    -- remove a common factor and use the Lipschitz assumption `B`
    refine' mul_le_mul_of_nonneg_left ((B this).trans _) (zero_le_one.trans (d0 a))
    -- ‚ä¢ dist Œ± (j z a) * M ‚â§ dist Œ± (j z a) * max (1 / Œµ) M
    exact mul_le_mul_of_nonneg_left (le_max_right _ M) dist_nonneg
    -- üéâ no goals
#align liouville.exists_one_le_pow_mul_dist Liouville.exists_one_le_pow_mul_dist

theorem exists_pos_real_of_irrational_root {Œ± : ‚Ñù} (ha : Irrational Œ±) {f : ‚Ñ§[X]} (f0 : f ‚â† 0)
    (fa : eval Œ± (map (algebraMap ‚Ñ§ ‚Ñù) f) = 0) :
    ‚àÉ A : ‚Ñù, 0 < A ‚àß ‚àÄ a : ‚Ñ§, ‚àÄ b : ‚Ñï,
      (1 : ‚Ñù) ‚â§ ((b : ‚Ñù) + 1) ^ f.natDegree * (|Œ± - a / (b + 1)| * A) := by
  -- `fR` is `f` viewed as a polynomial with `‚Ñù` coefficients.
  set fR : ‚Ñù[X] := map (algebraMap ‚Ñ§ ‚Ñù) f
  -- ‚ä¢ ‚àÉ A, 0 < A ‚àß ‚àÄ (a : ‚Ñ§) (b : ‚Ñï), 1 ‚â§ (‚Üëb + 1) ^ natDegree f * (|Œ± - ‚Üëa / (‚Üëb  ‚Ä¶
  -- `fR` is non-zero, since `f` is non-zero.
  obtain fR0 : fR ‚â† 0 := fun fR0 =>
    (map_injective (algebraMap ‚Ñ§ ‚Ñù) fun _ _ A => Int.cast_inj.mp A).ne f0
      (fR0.trans (Polynomial.map_zero _).symm)
  -- reformulating assumption `fa`: `Œ±` is a root of `fR`.
  have ar : Œ± ‚àà (fR.roots.toFinset : Set ‚Ñù) :=
    Finset.mem_coe.mpr (Multiset.mem_toFinset.mpr ((mem_roots fR0).mpr (IsRoot.def.mpr fa)))
  -- Since the polynomial `fR` has finitely many roots, there is a closed interval centered at `Œ±`
  -- such that `Œ±` is the only root of `fR` in the interval.
  obtain ‚ü®Œ∂, z0, U‚ü© : ‚àÉ Œ∂ > 0, closedBall Œ± Œ∂ ‚à© fR.roots.toFinset = {Œ±} :=
    @exists_closedBall_inter_eq_singleton_of_discrete _ _ _ discrete_of_t1_of_finite _ ar
  -- Since `fR` is continuous, it is bounded on the interval above.
  obtain ‚ü®xm, -, hM‚ü© : ‚àÉ xm : ‚Ñù, xm ‚àà Icc (Œ± - Œ∂) (Œ± + Œ∂) ‚àß
      IsMaxOn (|fR.derivative.eval ¬∑|) (Icc (Œ± - Œ∂) (Œ± + Œ∂)) xm :=
    IsCompact.exists_isMaxOn isCompact_Icc
      ‚ü®Œ±, (sub_lt_self Œ± z0).le, (lt_add_of_pos_right Œ± z0).le‚ü©
      (continuous_abs.comp fR.derivative.continuous_aeval).continuousOn
  -- Use the key lemma `exists_one_le_pow_mul_dist`: we are left to show that ...
  refine'
    @exists_one_le_pow_mul_dist ‚Ñ§ ‚Ñï ‚Ñù _ _ _ (fun y => fR.eval y) Œ± Œ∂ |fR.derivative.eval xm| _ z0
      (fun y hy => _) fun z a hq => _
  -- 1: the denominators are positive -- essentially by definition;
  ¬∑ exact fun a => one_le_pow_of_one_le ((le_add_iff_nonneg_left 1).mpr a.cast_nonneg) _
    -- üéâ no goals
  -- 2: the polynomial `fR` is Lipschitz at `Œ±` -- as its derivative continuous;
  ¬∑ rw [mul_comm]
    -- ‚ä¢ dist ((fun y => eval y fR) Œ±) ((fun y => eval y fR) y) ‚â§ |eval xm (‚Üëderivati ‚Ä¶
    rw [Real.closedBall_eq_Icc] at hy
    -- ‚ä¢ dist ((fun y => eval y fR) Œ±) ((fun y => eval y fR) y) ‚â§ |eval xm (‚Üëderivati ‚Ä¶
    -- apply the Mean Value Theorem: the bound on the derivative comes from differentiability.
    refine'
      Convex.norm_image_sub_le_of_norm_deriv_le (fun _ _ => fR.differentiableAt)
        (fun y h => by rw [fR.deriv]; exact hM h) (convex_Icc _ _) hy (mem_Icc_iff_abs_le.mp _)
    exact @mem_closedBall_self ‚Ñù _ Œ± Œ∂ (le_of_lt z0)
    -- üéâ no goals
  -- 3: the weird inequality of Liouville type with powers of the denominators.
  ¬∑ show 1 ‚â§ (a + 1 : ‚Ñù) ^ f.natDegree * |eval Œ± fR - eval ((z : ‚Ñù) / (a + 1)) fR|
    -- ‚ä¢ 1 ‚â§ (‚Üëa + 1) ^ natDegree f * |eval Œ± fR - eval (‚Üëz / (‚Üëa + 1)) fR|
    rw [fa, zero_sub, abs_neg]
    -- ‚ä¢ 1 ‚â§ (‚Üëa + 1) ^ natDegree f * |eval (‚Üëz / (‚Üëa + 1)) fR|
    rw [show (a + 1 : ‚Ñù) = ((a + 1 : ‚Ñï) : ‚Ñ§) by norm_cast] at hq ‚ä¢
    -- ‚ä¢ 1 ‚â§ ‚Üë‚Üë(a + 1) ^ natDegree f * |eval (‚Üëz / ‚Üë‚Üë(a + 1)) fR|
    -- key observation: the right-hand side of the inequality is an *integer*.  Therefore,
    -- if its absolute value is not at least one, then it vanishes.  Proceed by contradiction
    refine' one_le_pow_mul_abs_eval_div (Int.coe_nat_succ_pos a) fun hy => _
    -- ‚ä¢ False
    -- As the evaluation of the polynomial vanishes, we found a root of `fR` that is rational.
    -- We know that `Œ±` is the only root of `fR` in our interval, and `Œ±` is irrational:
    -- follow your nose.
    refine' (irrational_iff_ne_rational Œ±).mp ha z (a + 1) (mem_singleton_iff.mp _).symm
    -- ‚ä¢ ‚Üëz / ‚Üë(‚Üëa + 1) ‚àà {Œ±}
    refine' U.subset _
    -- ‚ä¢ ‚Üëz / ‚Üë(‚Üëa + 1) ‚àà closedBall Œ± Œ∂ ‚à© ‚Üë(Multiset.toFinset (roots fR))
    refine' ‚ü®hq, Finset.mem_coe.mp (Multiset.mem_toFinset.mpr _)‚ü©
    -- ‚ä¢ ‚Üëz / ‚Üë(‚Üëa + 1) ‚àà roots fR
    exact (mem_roots fR0).mpr (IsRoot.def.mpr hy)
    -- üéâ no goals
#align liouville.exists_pos_real_of_irrational_root Liouville.exists_pos_real_of_irrational_root

/-- **Liouville's Theorem** -/
protected theorem transcendental {x : ‚Ñù} (lx : Liouville x) : Transcendental ‚Ñ§ x := by
  -- Proceed by contradiction: if `x` is algebraic, then `x` is the root (`ef0`) of a
  -- non-zero (`f0`) polynomial `f`
  rintro ‚ü®f : ‚Ñ§[X], f0, ef0‚ü©
  -- ‚ä¢ False
  -- Change `aeval x f = 0` to `eval (map _ f) = 0`, who knew.
  replace ef0 : (f.map (algebraMap ‚Ñ§ ‚Ñù)).eval x = 0;
  -- ‚ä¢ eval x (map (algebraMap ‚Ñ§ ‚Ñù) f) = 0
  ¬∑ rwa [aeval_def, ‚Üê eval_map] at ef0
    -- üéâ no goals
  -- There is a "large" real number `A` such that `(b + 1) ^ (deg f) * |f (x - a / (b + 1))| * A`
  -- is at least one.  This is obtained from lemma `exists_pos_real_of_irrational_root`.
  obtain ‚ü®A, hA, h‚ü© : ‚àÉ A : ‚Ñù, 0 < A ‚àß ‚àÄ (a : ‚Ñ§) (b : ‚Ñï),
      (1 : ‚Ñù) ‚â§ ((b : ‚Ñù) + 1) ^ f.natDegree * (|x - a / (b + 1)| * A) :=
    exists_pos_real_of_irrational_root lx.irrational f0 ef0
  -- Since the real numbers are Archimedean, a power of `2` exceeds `A`: `hn : A < 2 ^ r`.
  rcases pow_unbounded_of_one_lt A (lt_add_one 1) with ‚ü®r, hn‚ü©
  -- ‚ä¢ False
  -- Use the Liouville property, with exponent `r + deg f`.
  obtain ‚ü®a, b, b1, -, a1‚ü© : ‚àÉ a b : ‚Ñ§, 1 < b ‚àß x ‚â† a / b ‚àß
      |x - a / b| < 1 / (b : ‚Ñù) ^ (r + f.natDegree) :=
    lx (r + f.natDegree)
  have b0 : (0 : ‚Ñù) < b := zero_lt_one.trans (by rw [‚Üê Int.cast_one]; exact Int.cast_lt.mpr b1)
  -- ‚ä¢ False
  -- Prove that `b ^ f.nat_degree * abs (x - a / b)` is strictly smaller than itself
  -- recall, this is a proof by contradiction!
  refine' lt_irrefl ((b : ‚Ñù) ^ f.natDegree * |x - ‚Üëa / ‚Üëb|) _
  -- ‚ä¢ ‚Üëb ^ natDegree f * |x - ‚Üëa / ‚Üëb| < ‚Üëb ^ natDegree f * |x - ‚Üëa / ‚Üëb|
  -- clear denominators at `a1`
  rw [lt_div_iff' (pow_pos b0 _), pow_add, mul_assoc] at a1
  -- ‚ä¢ ‚Üëb ^ natDegree f * |x - ‚Üëa / ‚Üëb| < ‚Üëb ^ natDegree f * |x - ‚Üëa / ‚Üëb|
  -- split the inequality via `1 / A`.
  refine' (_ : (b : ‚Ñù) ^ f.natDegree * |x - a / b| < 1 / A).trans_le _
  -- ‚ä¢ ‚Üëb ^ natDegree f * |x - ‚Üëa / ‚Üëb| < 1 / A
  -- This branch of the proof uses the Liouville condition and the Archimedean property
  ¬∑ refine' (lt_div_iff' hA).mpr _
    -- ‚ä¢ A * (‚Üëb ^ natDegree f * |x - ‚Üëa / ‚Üëb|) < 1
    refine' lt_of_le_of_lt _ a1
    -- ‚ä¢ A * (‚Üëb ^ natDegree f * |x - ‚Üëa / ‚Üëb|) ‚â§ ‚Üëb ^ r * (‚Üëb ^ natDegree f * |x - ‚Üë ‚Ä¶
    gcongr
    -- ‚ä¢ A ‚â§ ‚Üëb ^ r
    refine' hn.le.trans _
    -- ‚ä¢ (1 + 1) ^ r ‚â§ ‚Üëb ^ r
    rw [one_add_one_eq_two]
    -- ‚ä¢ 2 ^ r ‚â§ ‚Üëb ^ r
    gcongr
    -- ‚ä¢ 2 ‚â§ ‚Üëb
    exact Int.cast_two.symm.le.trans (Int.cast_le.mpr (Int.add_one_le_iff.mpr b1))
    -- üéâ no goals
  -- this branch of the proof exploits the "integrality" of evaluations of polynomials
  -- at ratios of integers.
  ¬∑ lift b to ‚Ñï using zero_le_one.trans b1.le
    -- ‚ä¢ 1 / A ‚â§ ‚Üë‚Üëb ^ natDegree f * |x - ‚Üëa / ‚Üë‚Üëb|
    specialize h a b.pred
    -- ‚ä¢ 1 / A ‚â§ ‚Üë‚Üëb ^ natDegree f * |x - ‚Üëa / ‚Üë‚Üëb|
    rwa [‚Üê Nat.cast_succ, Nat.succ_pred_eq_of_pos (zero_lt_one.trans _), ‚Üê mul_assoc, ‚Üê
      div_le_iff hA] at h
    exact Int.ofNat_lt.mp b1
    -- üéâ no goals
#align liouville.transcendental Liouville.transcendental

end Liouville
