/-
Copyright (c) 2021 Ashvni Narayanan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ashvni Narayanan, Anne Baanen
-/
import Mathlib.Data.Int.Parity
import Mathlib.RingTheory.DedekindDomain.IntegralClosure

#align_import number_theory.number_field.basic from "leanprover-community/mathlib"@"f0c8bf9245297a541f468be517f1bde6195105e9"

/-!
# Number fields
This file defines a number field and the ring of integers corresponding to it.

## Main definitions
 - `NumberField` defines a number field as a field which has characteristic zero and is finite
    dimensional over â„š.
 - `RingOfIntegers` defines the ring of integers (or number ring) corresponding to a number field
    as the integral closure of â„¤ in the number field.

## Implementation notes
The definitions that involve a field of fractions choose a canonical field of fractions,
but are independent of that choice.

## References
* [D. Marcus, *Number Fields*][marcus1977number]
* [J.W.S. Cassels, A. FrÃ¶lich, *Algebraic Number Theory*][cassels1967algebraic]
* [P. Samuel, *Algebraic Theory of Numbers*][samuel1970algebraic]

## Tags
number field, ring of integers
-/


/-- A number field is a field which has characteristic zero and is finite
dimensional over â„š. -/
class NumberField (K : Type*) [Field K] : Prop where
  [to_charZero : CharZero K]
  [to_finiteDimensional : FiniteDimensional â„š K]
#align number_field NumberField

open Function Module

open scoped Classical nonZeroDivisors

/-- `â„¤` with its usual ring structure is not a field. -/
theorem Int.not_isField : Â¬IsField â„¤ := fun h =>
  Int.not_even_one <|
    (h.mul_inv_cancel two_ne_zero).imp fun a => by rw [â† two_mul]; exact Eq.symm
#align int.not_is_field Int.not_isField

namespace NumberField

variable (K L : Type*) [Field K] [Field L] [nf : NumberField K]

-- See note [lower instance priority]
attribute [instance] NumberField.to_charZero NumberField.to_finiteDimensional

protected theorem isAlgebraic : Algebra.IsAlgebraic â„š K :=
  Algebra.IsAlgebraic.of_finite _ _
#align number_field.is_algebraic NumberField.isAlgebraic

instance [NumberField L] [Algebra K L] : FiniteDimensional K L :=
  Module.Finite.of_restrictScalars_finite â„š K L

/-- The ring of integers (or number ring) corresponding to a number field
is the integral closure of â„¤ in the number field.

This is defined as its own type, rather than a `Subalgebra`, for performance reasons:
looking for instances of the form `SMul (RingOfIntegers _) (RingOfIntegers _)` makes
much more effective use of the discrimination tree than instances of the form
`SMul (Subtype _) (Subtype _)`.
The drawback is we have to copy over instances manually.
-/
def RingOfIntegers : Type _ :=
  integralClosure â„¤ K
#align number_field.ring_of_integers NumberField.RingOfIntegers

@[inherit_doc] scoped notation "ğ“" => NumberField.RingOfIntegers

#noalign number_field.mem_ring_of_integers

namespace RingOfIntegers

instance : CommRing (ğ“ K) :=
  inferInstanceAs (CommRing (integralClosure _ _))
instance : IsDomain (ğ“ K) :=
  inferInstanceAs (IsDomain (integralClosure _ _))
instance : CharZero (ğ“ K) :=
  inferInstanceAs (CharZero (integralClosure _ _))
instance : Algebra (ğ“ K) K :=
  inferInstanceAs (Algebra (integralClosure _ _) _)
instance : NoZeroSMulDivisors (ğ“ K) K :=
  inferInstanceAs (NoZeroSMulDivisors (integralClosure _ _) _)
instance : Nontrivial (ğ“ K) :=
  inferInstanceAs (Nontrivial (integralClosure _ _))
instance {L : Type*} [Ring L] [Algebra K L] : Algebra (ğ“ K) L :=
  inferInstanceAs (Algebra (integralClosure _ _) L)
instance {L : Type*} [Ring L] [Algebra K L] :  IsScalarTower (ğ“ K) K L :=
  inferInstanceAs (IsScalarTower (integralClosure _ _) K L)

variable {K}

/-- The canonical coercion from `ğ“ K` to `K`. -/
@[coe]
abbrev val (x : ğ“ K) : K := algebraMap _ _ x

/-- This instance has to be `CoeHead` because we only want to apply it from `ğ“ K` to `K`. -/
instance : CoeHead (ğ“ K) K := âŸ¨valâŸ©

lemma coe_eq_algebraMap (x : ğ“ K) : (x : K) = algebraMap _ _ x := rfl

@[ext] theorem ext {x y : ğ“ K} (h : (x : K) = (y : K)) : x = y :=
  Subtype.ext h

theorem ext_iff {x y : ğ“ K} : x = y â†” (x : K) = (y : K) :=
  Subtype.ext_iff

@[norm_cast]
theorem eq_iff {x y : ğ“ K} : (x : K) = (y : K) â†” x = y :=
  NumberField.RingOfIntegers.ext_iff.symm

@[simp] lemma map_mk (x : K) (hx) : algebraMap (ğ“ K) K âŸ¨x, hxâŸ© = x := rfl

lemma coe_mk {x : K} (hx) : ((âŸ¨x, hxâŸ© : ğ“ K) : K) = x := rfl

lemma mk_eq_mk (x y : K) (hx hy) : (âŸ¨x, hxâŸ© : ğ“ K) = âŸ¨y, hyâŸ© â†” x = y := by simp

@[simp] lemma mk_one : (âŸ¨1, one_mem _âŸ© : ğ“ K) = 1 :=
  rfl

@[simp] lemma mk_zero : (âŸ¨0, zero_mem _âŸ© : ğ“ K) = 0 :=
  rfl
-- TODO: these lemmas don't seem to fire?
@[simp] lemma mk_add_mk (x y : K) (hx hy) : (âŸ¨x, hxâŸ© : ğ“ K) + âŸ¨y, hyâŸ© = âŸ¨x + y, add_mem hx hyâŸ© :=
  rfl

@[simp] lemma mk_mul_mk (x y : K) (hx hy) : (âŸ¨x, hxâŸ© : ğ“ K) * âŸ¨y, hyâŸ© = âŸ¨x * y, mul_mem hx hyâŸ© :=
  rfl

@[simp] lemma mk_sub_mk (x y : K) (hx hy) : (âŸ¨x, hxâŸ© : ğ“ K) - âŸ¨y, hyâŸ© = âŸ¨x - y, sub_mem hx hyâŸ© :=
  rfl

@[simp] lemma neg_mk (x : K) (hx) : (-âŸ¨x, hxâŸ© : ğ“ K) = âŸ¨-x, neg_mem hxâŸ© :=
  rfl

end RingOfIntegers

/-- Given an algebra between two fields, create an algebra between their two rings of integers. -/
instance inst_ringOfIntegersAlgebra [Algebra K L] : Algebra (ğ“ K) (ğ“ L) :=
  RingHom.toAlgebra
    { toFun := fun k => âŸ¨algebraMap K L (algebraMap _ K k), IsIntegral.algebraMap k.2âŸ©
      map_zero' := by ext; simp only [RingOfIntegers.map_mk, map_zero]
      map_one' := by ext; simp only [RingOfIntegers.map_mk, map_one]
      map_add' := fun x y => by ext; simp only [RingOfIntegers.map_mk, map_add]
      map_mul' := fun x y => by ext; simp only [RingOfIntegers.map_mk, map_mul] }
#align number_field.ring_of_integers_algebra NumberField.inst_ringOfIntegersAlgebra

-- diamond at `reducible_and_instances` #10906
example : Algebra.id (ğ“ K) = inst_ringOfIntegersAlgebra K K := rfl

namespace RingOfIntegers

variable {K}

/-- The canonical map from `ğ“ K` to `K` is injective.

This is a convenient abbreviation for `NoZeroSMulDivisors.algebraMap_injective`.
-/
lemma coe_injective : Function.Injective (algebraMap (ğ“ K) K) :=
  NoZeroSMulDivisors.algebraMap_injective _ _

/-- The canonical map from `ğ“ K` to `K` is injective.

This is a convenient abbreviation for `map_eq_zero_iff` applied to
`NoZeroSMulDivisors.algebraMap_injective`.
-/
@[simp] lemma coe_eq_zero_iff {x : ğ“ K} : algebraMap _ K x = 0 â†” x = 0 :=
  map_eq_zero_iff _ coe_injective

/-- The canonical map from `ğ“ K` to `K` is injective.

This is a convenient abbreviation for `map_ne_zero_iff` applied to
`NoZeroSMulDivisors.algebraMap_injective`.
-/
lemma coe_ne_zero_iff {x : ğ“ K} : algebraMap _ K x â‰  0 â†” x â‰  0 :=
  map_ne_zero_iff _ coe_injective

theorem isIntegral_coe (x : ğ“ K) : IsIntegral â„¤ (algebraMap _ K x) :=
  x.2
#align number_field.ring_of_integers.is_integral_coe NumberField.RingOfIntegers.isIntegral_coe

theorem isIntegral (x : ğ“ K) : IsIntegral â„¤ x := by
  obtain âŸ¨P, hPm, hPâŸ© := x.isIntegral_coe
  refine âŸ¨P, hPm, ?_âŸ©
  rwa [IsScalarTower.algebraMap_eq (S := ğ“ K), â† Polynomial.hom_evalâ‚‚, coe_eq_zero_iff] at hP
#align number_field.is_integral_of_mem_ring_of_integers NumberField.RingOfIntegers.isIntegral

instance [NumberField K] : IsFractionRing (ğ“ K) K :=
  integralClosure.isFractionRing_of_finite_extension â„š _

instance : IsIntegralClosure (ğ“ K) â„¤ K :=
  integralClosure.isIntegralClosure _ _

instance [NumberField K] : IsIntegrallyClosed (ğ“ K) :=
  integralClosure.isIntegrallyClosedOfFiniteExtension â„š

#noalign number_field.ring_of_integers.map_mem

/-- The ring of integers of `K` are equivalent to any integral closure of `â„¤` in `K` -/
protected noncomputable def equiv (R : Type*) [CommRing R] [Algebra R K]
    [IsIntegralClosure R â„¤ K] : ğ“ K â‰ƒ+* R :=
  (IsIntegralClosure.equiv â„¤ R K _).symm.toRingEquiv
#align number_field.ring_of_integers.equiv NumberField.RingOfIntegers.equiv

variable (K)

instance [CharZero K] : CharZero (ğ“ K) :=
  CharZero.of_module _ K

instance : IsNoetherian â„¤ (ğ“ K) :=
  IsIntegralClosure.isNoetherian _ â„š K _

/-- The ring of integers of a number field is not a field. -/
theorem not_isField : Â¬IsField (ğ“ K) := by
  have h_inj : Function.Injective (algebraMap â„¤ (ğ“ K)) := RingHom.injective_int (algebraMap â„¤ (ğ“ K))
  intro hf
  exact Int.not_isField
    (((IsIntegralClosure.isIntegral_algebra â„¤ K).isField_iff_isField h_inj).mpr hf)
#align number_field.ring_of_integers.not_is_field NumberField.RingOfIntegers.not_isField

instance : IsDedekindDomain (ğ“ K) :=
  IsIntegralClosure.isDedekindDomain â„¤ â„š K _

instance : Free â„¤ (ğ“ K) :=
  IsIntegralClosure.module_free â„¤ â„š K (ğ“ K)

instance : IsLocalization (Algebra.algebraMapSubmonoid (ğ“ K) â„¤â°) K :=
  IsIntegralClosure.isLocalization_of_isSeparable â„¤ â„š K (ğ“ K)

/-- A â„¤-basis of the ring of integers of `K`. -/
noncomputable def basis : Basis (Free.ChooseBasisIndex â„¤ (ğ“ K)) â„¤ (ğ“ K) :=
  Free.chooseBasis â„¤ (ğ“ K)
#align number_field.ring_of_integers.basis NumberField.RingOfIntegers.basis

variable {K} {M : Type*}

/-- Given `f : M â†’ K` such that `âˆ€ x, IsIntegral â„¤ (f x)`, the corresponding function
`M â†’ ğ“ K`. -/
def restrict (f : M â†’ K) (h : âˆ€ x, IsIntegral â„¤ (f x)) (x : M) : ğ“ K :=
  âŸ¨f x, h xâŸ©

/-- Given `f : M â†’+ K` such that `âˆ€ x, IsIntegral â„¤ (f x)`, the corresponding function
`M â†’+ ğ“ K`. -/
def restrict_addMonoidHom [AddZeroClass M] (f : M â†’+ K) (h : âˆ€ x, IsIntegral â„¤ (f x)) :
    M â†’+ ğ“ K where
  toFun := restrict f h
  map_zero' := by simp only [restrict, map_zero, mk_zero]
  map_add' x y := by simp only [restrict, map_add, mk_add_mk _]

/-- Given `f : M â†’* K` such that `âˆ€ x, IsIntegral â„¤ (f x)`, the corresponding function
`M â†’* ğ“ K`. -/
@[to_additive existing] -- TODO: why doesn't it figure this out by itself?
def restrict_monoidHom [MulOneClass M] (f : M â†’* K) (h : âˆ€ x, IsIntegral â„¤ (f x)) : M â†’* ğ“ K where
  toFun := restrict f h
  map_one' := by simp only [restrict, map_one, mk_one]
  map_mul' x y := by simp only [restrict, map_mul, mk_mul_mk _]

end RingOfIntegers

/-- A basis of `K` over `â„š` that is also a basis of `ğ“ K` over `â„¤`. -/
noncomputable def integralBasis : Basis (Free.ChooseBasisIndex â„¤ (ğ“ K)) â„š K :=
  Basis.localizationLocalization â„š (nonZeroDivisors â„¤) K (RingOfIntegers.basis K)
#align number_field.integral_basis NumberField.integralBasis

@[simp]
theorem integralBasis_apply (i : Free.ChooseBasisIndex â„¤ (ğ“ K)) :
    integralBasis K i = algebraMap (ğ“ K) K (RingOfIntegers.basis K i) :=
  Basis.localizationLocalization_apply â„š (nonZeroDivisors â„¤) K (RingOfIntegers.basis K) i
#align number_field.integral_basis_apply NumberField.integralBasis_apply

@[simp]
theorem integralBasis_repr_apply (x : (ğ“ K)) (i : Free.ChooseBasisIndex â„¤ (ğ“ K)) :
    (integralBasis K).repr (algebraMap _ _ x) i =
      (algebraMap â„¤ â„š) ((RingOfIntegers.basis K).repr x i) :=
  Basis.localizationLocalization_repr_algebraMap â„š (nonZeroDivisors â„¤) K _ x i

theorem mem_span_integralBasis {x : K} :
    x âˆˆ Submodule.span â„¤ (Set.range (integralBasis K)) â†” x âˆˆ (algebraMap (ğ“ K) K).range := by
  rw [integralBasis, Basis.localizationLocalization_span, LinearMap.mem_range,
      IsScalarTower.coe_toAlgHom', RingHom.mem_range]

theorem RingOfIntegers.rank : FiniteDimensional.finrank â„¤ (ğ“ K) = FiniteDimensional.finrank â„š K :=
  IsIntegralClosure.rank â„¤ â„š K (ğ“ K)
#align number_field.ring_of_integers.rank NumberField.RingOfIntegers.rank

end NumberField

namespace Rat

open NumberField

instance numberField : NumberField â„š where
  to_charZero := inferInstance
  to_finiteDimensional := by
  -- The vector space structure of `â„š` over itself can arise in multiple ways:
  -- all fields are vector spaces over themselves (used in `Rat.finiteDimensional`)
  -- all char 0 fields have a canonical embedding of `â„š` (used in `NumberField`).
  -- Show that these coincide:
    convert (inferInstance : FiniteDimensional â„š â„š)
#align rat.number_field Rat.numberField

/-- The ring of integers of `â„š` as a number field is just `â„¤`. -/
noncomputable def ringOfIntegersEquiv : ğ“ â„š â‰ƒ+* â„¤ :=
  RingOfIntegers.equiv â„¤
#align rat.ring_of_integers_equiv Rat.ringOfIntegersEquiv

end Rat

namespace AdjoinRoot

section

open scoped Polynomial

attribute [-instance] algebraRat

/-- The quotient of `â„š[X]` by the ideal generated by an irreducible polynomial of `â„š[X]`
is a number field. -/
instance {f : Polynomial â„š} [hf : Fact (Irreducible f)] : NumberField (AdjoinRoot f) where
  to_charZero := charZero_of_injective_algebraMap (algebraMap â„š _).injective
  to_finiteDimensional := by convert (AdjoinRoot.powerBasis hf.out.ne_zero).finite

end

end AdjoinRoot
