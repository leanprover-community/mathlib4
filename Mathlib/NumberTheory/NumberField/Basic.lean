/-
Copyright (c) 2021 Ashvni Narayanan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ashvni Narayanan, Anne Baanen
-/
import Mathlib.Data.Int.Parity
import Mathlib.RingTheory.DedekindDomain.IntegralClosure

#align_import number_theory.number_field.basic from "leanprover-community/mathlib"@"f0c8bf9245297a541f468be517f1bde6195105e9"

/-!
# Number fields
This file defines a number field and the ring of integers corresponding to it.

## Main definitions
 - `NumberField` defines a number field as a field which has characteristic zero and is finite
    dimensional over ‚Ñö.
 - `RingOfIntegers` defines the ring of integers (or number ring) corresponding to a number field
    as the integral closure of ‚Ñ§ in the number field.

## Implementation notes
The definitions that involve a field of fractions choose a canonical field of fractions,
but are independent of that choice.

## References
* [D. Marcus, *Number Fields*][marcus1977number]
* [J.W.S. Cassels, A. Fr√∂lich, *Algebraic Number Theory*][cassels1967algebraic]
* [P. Samuel, *Algebraic Theory of Numbers*][samuel1970algebraic]

## Tags
number field, ring of integers
-/


/-- A number field is a field which has characteristic zero and is finite
dimensional over ‚Ñö. -/
class NumberField (K : Type*) [Field K] : Prop where
  [to_charZero : CharZero K]
  [to_finiteDimensional : FiniteDimensional ‚Ñö K]
#align number_field NumberField

open Function Module

open scoped Classical BigOperators nonZeroDivisors

/-- `‚Ñ§` with its usual ring structure is not a field. -/
theorem Int.not_isField : ¬¨IsField ‚Ñ§ := fun h =>
  Int.not_even_one <|
    (h.mul_inv_cancel two_ne_zero).imp fun a => by rw [‚Üê two_mul]; exact Eq.symm
#align int.not_is_field Int.not_isField

namespace NumberField

variable (K L : Type*) [Field K] [Field L] [nf : NumberField K]

-- See note [lower instance priority]
attribute [instance] NumberField.to_charZero NumberField.to_finiteDimensional

protected theorem isAlgebraic : Algebra.IsAlgebraic ‚Ñö K :=
  Algebra.IsAlgebraic.of_finite _ _
#align number_field.is_algebraic NumberField.isAlgebraic

instance [NumberField L] [Algebra K L] : FiniteDimensional K L :=
  Module.Finite.of_restrictScalars_finite ‚Ñö K L

/-- The ring of integers (or number ring) corresponding to a number field
is the integral closure of ‚Ñ§ in the number field. -/
def RingOfIntegers : Type _ :=
  integralClosure ‚Ñ§ K
#align number_field.ring_of_integers NumberField.RingOfIntegers

@[inherit_doc] scoped notation "ùìû" => NumberField.RingOfIntegers

namespace RingOfIntegers

instance : CommRing (ùìû K) :=
  inferInstanceAs (CommRing (integralClosure _ _))
instance : IsDomain (ùìû K) :=
  inferInstanceAs (IsDomain (integralClosure _ _))
instance : CharZero (ùìû K) :=
  inferInstanceAs (CharZero (integralClosure _ _))
instance : Algebra (ùìû K) K :=
  inferInstanceAs (Algebra (integralClosure _ _) _)
instance : NoZeroSMulDivisors (ùìû K) K :=
  inferInstanceAs (NoZeroSMulDivisors (integralClosure _ _) _)

variable {K}

@[ext] theorem ext {x y : ùìû K} (h : algebraMap _ K x = algebraMap _ K y) : x = y :=
  Subtype.ext h
theorem ext_iff {x y : ùìû K} : x = y ‚Üî algebraMap _ K x = algebraMap _ K y :=
  Subtype.ext_iff

@[simp] lemma map_mk (x : K) (hx) : algebraMap (ùìû K) K ‚ü®x, hx‚ü© = x := rfl

lemma mk_eq_mk (x y : K) (hx hy) : (‚ü®x, hx‚ü© : ùìû K) = ‚ü®y, hy‚ü© ‚Üî x = y := by simp

@[simp] lemma mk_one : (‚ü®1, one_mem _‚ü© : ùìû K) = 1 :=
  rfl

@[simp] lemma mk_zero : (‚ü®0, zero_mem _‚ü© : ùìû K) = 0 :=
  rfl
-- TODO: these lemmas don't seem to fire?
@[simp] lemma mk_add_mk (x y : K) (hx hy) : (‚ü®x, hx‚ü© : ùìû K) + ‚ü®y, hy‚ü© = ‚ü®x + y, add_mem hx hy‚ü© :=
  rfl

@[simp] lemma mk_mul_mk (x y : K) (hx hy) : (‚ü®x, hx‚ü© : ùìû K) * ‚ü®y, hy‚ü© = ‚ü®x * y, mul_mem hx hy‚ü© :=
  rfl

@[simp] lemma mk_sub_mk (x y : K) (hx hy) : (‚ü®x, hx‚ü© : ùìû K) - ‚ü®y, hy‚ü© = ‚ü®x - y, sub_mem hx hy‚ü© :=
  rfl

@[simp] lemma neg_mk (x : K) (hx) : (-‚ü®x, hx‚ü© : ùìû K) = ‚ü®-x, neg_mem hx‚ü© :=
  rfl

end RingOfIntegers

/-- Given an algebra between two fields, create an algebra between their two rings of integers. -/
instance inst_ringOfIntegersAlgebra [Algebra K L] : Algebra (ùìû K) (ùìû L) :=
  RingHom.toAlgebra
    { toFun := fun k => ‚ü®algebraMap K L (algebraMap _ K k), IsIntegral.algebraMap k.2‚ü©
      map_zero' := Subtype.ext <| by simp only [Subtype.coe_mk, Subalgebra.coe_zero, map_zero]
      map_one' := Subtype.ext <| by simp only [Subtype.coe_mk, Subalgebra.coe_one, map_one]
      map_add' := fun x y =>
        Subtype.ext <| by simp only; rw [map_add, map_add, RingOfIntegers.mk_add_mk]
      map_mul' := fun x y =>
        Subtype.ext <| by simp only; rw [map_mul, map_mul, RingOfIntegers.mk_mul_mk] }
#align number_field.ring_of_integers_algebra NumberField.inst_ringOfIntegersAlgebra

-- diamond at `reducible_and_instances` #10906
example : Algebra.id (ùìû K) = inst_ringOfIntegersAlgebra K K := rfl

namespace RingOfIntegers

variable {K}

theorem isIntegral {K : Type*} [Field K] (x : ùìû K) :
    IsIntegral ‚Ñ§ x := by
  obtain ‚ü®P, hPm, hP‚ü© := x.2
  refine' ‚ü®P, hPm, _‚ü©
  have : algebraMap _ K x = x.1 := rfl
  rwa [IsScalarTower.algebraMap_eq (S := ùìû K), ‚Üê this, ‚Üê Polynomial.hom_eval‚ÇÇ, map_eq_zero_iff]
    at hP
  ¬∑ apply NoZeroSMulDivisors.algebraMap_injective
#align number_field.is_integral_of_mem_ring_of_integers NumberField.RingOfIntegers.isIntegral

instance [NumberField K] : IsFractionRing (ùìû K) K :=
  integralClosure.isFractionRing_of_finite_extension ‚Ñö _

instance : IsIntegralClosure (ùìû K) ‚Ñ§ K :=
  integralClosure.isIntegralClosure _ _

instance [NumberField K] : IsIntegrallyClosed (ùìû K) :=
  integralClosure.isIntegrallyClosedOfFiniteExtension ‚Ñö

theorem isIntegral_coe (x : ùìû K) : IsIntegral ‚Ñ§ (algebraMap _ K x) :=
  x.2
#align number_field.ring_of_integers.is_integral_coe NumberField.RingOfIntegers.isIntegral_coe

#noalign number_field.ring_of_integers.map_mem

/-- The ring of integers of `K` are equivalent to any integral closure of `‚Ñ§` in `K` -/
protected noncomputable def equiv (R : Type*) [CommRing R] [Algebra R K]
    [IsIntegralClosure R ‚Ñ§ K] : ùìû K ‚âÉ+* R :=
  (IsIntegralClosure.equiv ‚Ñ§ R K _).symm.toRingEquiv
#align number_field.ring_of_integers.equiv NumberField.RingOfIntegers.equiv

variable (K)

instance : CharZero (ùìû K) :=
  CharZero.of_module _ K

instance : IsNoetherian ‚Ñ§ (ùìû K) :=
  IsIntegralClosure.isNoetherian _ ‚Ñö K _

/-- The ring of integers of a number field is not a field. -/
theorem not_isField : ¬¨IsField (ùìû K) := by
  have h_inj : Function.Injective (algebraMap ‚Ñ§ (ùìû K)) := RingHom.injective_int (algebraMap ‚Ñ§ (ùìû K))
  intro hf
  exact Int.not_isField
    (((IsIntegralClosure.isIntegral_algebra ‚Ñ§ K).isField_iff_isField h_inj).mpr hf)
#align number_field.ring_of_integers.not_is_field NumberField.RingOfIntegers.not_isField

instance : IsDedekindDomain (ùìû K) :=
  IsIntegralClosure.isDedekindDomain ‚Ñ§ ‚Ñö K _

instance : Free ‚Ñ§ (ùìû K) :=
  IsIntegralClosure.module_free ‚Ñ§ ‚Ñö K (ùìû K)

instance : IsLocalization (Algebra.algebraMapSubmonoid (ùìû K) ‚Ñ§‚Å∞) K :=
  IsIntegralClosure.isLocalization_of_isSeparable ‚Ñ§ ‚Ñö K (ùìû K)

/-- A ‚Ñ§-basis of the ring of integers of `K`. -/
noncomputable def basis : Basis (Free.ChooseBasisIndex ‚Ñ§ (ùìû K)) ‚Ñ§ (ùìû K) :=
  Free.chooseBasis ‚Ñ§ (ùìû K)
#align number_field.ring_of_integers.basis NumberField.RingOfIntegers.basis

variable {K} {M : Type*}

/-- Given `f : M ‚Üí K` such that `‚àÄ x, IsIntegral ‚Ñ§ (f x)`, the corresponding function
`M ‚Üí ùìû K`. -/
def restrict (f : M ‚Üí K) (h : ‚àÄ x, IsIntegral ‚Ñ§ (f x)) (x : M) : ùìû K :=
  ‚ü®f x, h x‚ü©

/-- Given `f : M ‚Üí+ K` such that `‚àÄ x, IsIntegral ‚Ñ§ (f x)`, the corresponding function
`M ‚Üí+ ùìû K`. -/
def restrict_addMonoidHom [AddZeroClass M] (f : M ‚Üí+ K) (h : ‚àÄ x, IsIntegral ‚Ñ§ (f x)) :
    M ‚Üí+ ùìû K where
  toFun := restrict f h
  map_zero' := by unfold restrict; rw [‚Üê mk_zero, mk_eq_mk, map_zero]
  map_add' x y := by unfold restrict; simp only [map_add]; rw [mk_add_mk]

/-- Given `f : M ‚Üí* K` such that `‚àÄ x, IsIntegral ‚Ñ§ (f x)`, the corresponding function
`M ‚Üí* ùìû K`. -/
@[to_additive existing] -- TODO: why doesn't it figure this out by itself?
def restrict_monoidHom [MulOneClass M] (f : M ‚Üí* K) (h : ‚àÄ x, IsIntegral ‚Ñ§ (f x)) : M ‚Üí* ùìû K where
  toFun := restrict f h
  map_one' := by unfold restrict; rw [‚Üê mk_one, mk_eq_mk, map_one]
  map_mul' x y := by unfold restrict; simp only [map_mul]; rw [mk_mul_mk]

end RingOfIntegers

/-- A basis of `K` over `‚Ñö` that is also a basis of `ùìû K` over `‚Ñ§`. -/
noncomputable def integralBasis : Basis (Free.ChooseBasisIndex ‚Ñ§ (ùìû K)) ‚Ñö K :=
  Basis.localizationLocalization ‚Ñö (nonZeroDivisors ‚Ñ§) K (RingOfIntegers.basis K)
#align number_field.integral_basis NumberField.integralBasis

@[simp]
theorem integralBasis_apply (i : Free.ChooseBasisIndex ‚Ñ§ (ùìû K)) :
    integralBasis K i = algebraMap (ùìû K) K (RingOfIntegers.basis K i) :=
  Basis.localizationLocalization_apply ‚Ñö (nonZeroDivisors ‚Ñ§) K (RingOfIntegers.basis K) i
#align number_field.integral_basis_apply NumberField.integralBasis_apply

@[simp]
theorem integralBasis_repr_apply (x : (ùìû K)) (i : Free.ChooseBasisIndex ‚Ñ§ (ùìû K)) :
    (integralBasis K).repr (algebraMap _ _ x) i =
      (algebraMap ‚Ñ§ ‚Ñö) ((RingOfIntegers.basis K).repr x i) :=
  Basis.localizationLocalization_repr_algebraMap ‚Ñö (nonZeroDivisors ‚Ñ§) K _ x i

theorem mem_span_integralBasis {x : K} :
    x ‚àà Submodule.span ‚Ñ§ (Set.range (integralBasis K)) ‚Üî x ‚àà (algebraMap (ùìû K) K).range := by
  rw [integralBasis, Basis.localizationLocalization_span, LinearMap.mem_range,
      IsScalarTower.coe_toAlgHom', RingHom.mem_range]

theorem RingOfIntegers.rank : FiniteDimensional.finrank ‚Ñ§ (ùìû K) = FiniteDimensional.finrank ‚Ñö K :=
  IsIntegralClosure.rank ‚Ñ§ ‚Ñö K (ùìû K)
#align number_field.ring_of_integers.rank NumberField.RingOfIntegers.rank

end NumberField

namespace Rat

open NumberField

instance numberField : NumberField ‚Ñö where
  to_charZero := inferInstance
  to_finiteDimensional := by
  -- The vector space structure of `‚Ñö` over itself can arise in multiple ways:
  -- all fields are vector spaces over themselves (used in `Rat.finiteDimensional`)
  -- all char 0 fields have a canonical embedding of `‚Ñö` (used in `NumberField`).
  -- Show that these coincide:
    convert (inferInstance : FiniteDimensional ‚Ñö ‚Ñö)
#align rat.number_field Rat.numberField

/-- The ring of integers of `‚Ñö` as a number field is just `‚Ñ§`. -/
noncomputable def ringOfIntegersEquiv : ùìû ‚Ñö ‚âÉ+* ‚Ñ§ :=
  RingOfIntegers.equiv ‚Ñ§
#align rat.ring_of_integers_equiv Rat.ringOfIntegersEquiv

end Rat

namespace AdjoinRoot

section

open scoped Polynomial

attribute [-instance] algebraRat

/-- The quotient of `‚Ñö[X]` by the ideal generated by an irreducible polynomial of `‚Ñö[X]`
is a number field. -/
instance {f : Polynomial ‚Ñö} [hf : Fact (Irreducible f)] : NumberField (AdjoinRoot f) where
  to_charZero := charZero_of_injective_algebraMap (algebraMap ‚Ñö _).injective
  to_finiteDimensional := by convert (AdjoinRoot.powerBasis hf.out.ne_zero).finiteDimensional

end

end AdjoinRoot
