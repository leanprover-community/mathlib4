/-
Copyright (c) 2021 Ashvni Narayanan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ashvni Narayanan, Anne Baanen
-/
import Mathlib.Algebra.Algebra.Rat
import Mathlib.Algebra.CharZero.AddMonoidHom
import Mathlib.Algebra.Ring.Int.Parity
import Mathlib.Algebra.Ring.Int.Units
import Mathlib.RingTheory.DedekindDomain.IntegralClosure

/-!
# Number fields
This file defines a number field and the ring of integers corresponding to it.

## Main definitions
- `NumberField` defines a number field as a field which has characteristic zero and is finite
  dimensional over â„š.
- `RingOfIntegers` defines the ring of integers (or number ring) corresponding to a number field
  as the integral closure of â„¤ in the number field.

## Implementation notes
The definitions that involve a field of fractions choose a canonical field of fractions,
but are independent of that choice.

## References
* [D. Marcus, *Number Fields*][marcus1977number]
* [J.W.S. Cassels, A. FrÃ¶hlich, *Algebraic Number Theory*][cassels1967algebraic]
* [P. Samuel, *Algebraic Theory of Numbers*][samuel1967]

## Tags
number field, ring of integers
-/


/-- A number field is a field which has characteristic zero and is finite
dimensional over â„š. -/
@[stacks 09GA]
class NumberField (K : Type*) [Field K] : Prop where
  [to_charZero : CharZero K]
  [to_finiteDimensional : FiniteDimensional â„š K]

open Function Module

open scoped nonZeroDivisors

/-- `â„¤` with its usual ring structure is not a field. -/
theorem Int.not_isField : Â¬IsField â„¤ := fun h =>
  Int.not_even_one <|
    (h.mul_inv_cancel two_ne_zero).imp fun a => by rw [â† two_mul]; exact Eq.symm

namespace NumberField

variable (K L : Type*) [Field K] [Field L]

-- See note [lower instance priority]
attribute [instance] NumberField.to_charZero NumberField.to_finiteDimensional

protected theorem isAlgebraic [NumberField K] : Algebra.IsAlgebraic â„š K :=
  Algebra.IsAlgebraic.of_finite _ _

instance [NumberField K] [NumberField L] [Algebra K L] : FiniteDimensional K L :=
  Module.Finite.of_restrictScalars_finite â„š K L

/-- A finite extension of a number field is a number field. -/
theorem of_module_finite [NumberField K] [Algebra K L] [Module.Finite K L] : NumberField L where
  to_charZero := charZero_of_injective_algebraMap (algebraMap K L).injective
  to_finiteDimensional :=
    letI := charZero_of_injective_algebraMap (algebraMap K L).injective
    Module.Finite.trans K L

variable {K} {L} in
instance of_intermediateField [NumberField K] [NumberField L] [Algebra K L]
    (E : IntermediateField K L) : NumberField E :=
  of_module_finite K E

variable {K} in
instance of_subfield [NumberField K] (E : Subfield K) : NumberField E where
  to_finiteDimensional := FiniteDimensional.left â„š E K

theorem of_tower [NumberField K] [NumberField L] [Algebra K L] (E : Type*) [Field E]
    [Algebra K E] [Algebra E L] [IsScalarTower K E L] : NumberField E :=
  letI := Module.Finite.left K E L
  of_module_finite K E

theorem of_ringEquiv (e : K â‰ƒ+* L) [NumberField K] : NumberField L :=
  letI := CharZero.of_addMonoidHom e.toAddMonoidHom (by simp) e.injective
  {
    to_charZero := inferInstance
    to_finiteDimensional := (e : K â‰ƒâ‚—[â„š] L).finiteDimensional
  }
/-- The ring of integers (or number ring) corresponding to a number field
is the integral closure of â„¤ in the number field.

This is defined as its own type, rather than a `Subalgebra`, for performance reasons:
looking for instances of the form `SMul (RingOfIntegers _) (RingOfIntegers _)` makes
much more effective use of the discrimination tree than instances of the form
`SMul (Subtype _) (Subtype _)`.
The drawback is we have to copy over instances manually.
-/
def RingOfIntegers : Type _ :=
  integralClosure â„¤ K

@[inherit_doc] scoped notation "ğ“" => NumberField.RingOfIntegers

namespace RingOfIntegers

instance : CommRing (ğ“ K) :=
  inferInstanceAs (CommRing (integralClosure _ _))
instance : IsDomain (ğ“ K) :=
  inferInstanceAs (IsDomain (integralClosure _ _))
instance [NumberField K] : CharZero (ğ“ K) :=
  inferInstanceAs (CharZero (integralClosure _ _))
instance : Algebra (ğ“ K) K :=
  inferInstanceAs (Algebra (integralClosure _ _) _)
instance : NoZeroSMulDivisors (ğ“ K) K :=
  inferInstanceAs (NoZeroSMulDivisors (integralClosure _ _) _)
instance : Nontrivial (ğ“ K) :=
  inferInstanceAs (Nontrivial (integralClosure _ _))
instance {L : Type*} [Ring L] [Algebra K L] : Algebra (ğ“ K) L :=
  inferInstanceAs (Algebra (integralClosure _ _) L)
instance {L : Type*} [Ring L] [Algebra K L] : IsScalarTower (ğ“ K) K L :=
  inferInstanceAs (IsScalarTower (integralClosure _ _) K L)

variable {K}

/-- The canonical coercion from `ğ“ K` to `K`. -/
@[coe]
abbrev val (x : ğ“ K) : K := algebraMap _ _ x

/-- This instance has to be `CoeHead` because we only want to apply it from `ğ“ K` to `K`. -/
instance : CoeHead (ğ“ K) K := âŸ¨valâŸ©

lemma coe_eq_algebraMap (x : ğ“ K) : (x : K) = algebraMap _ _ x := rfl

@[ext] theorem ext {x y : ğ“ K} (h : (x : K) = (y : K)) : x = y :=
  Subtype.ext h

@[norm_cast]
theorem eq_iff {x y : ğ“ K} : (x : K) = (y : K) â†” x = y :=
  NumberField.RingOfIntegers.ext_iff.symm

@[simp] lemma map_mk (x : K) (hx) : algebraMap (ğ“ K) K âŸ¨x, hxâŸ© = x := rfl

lemma coe_mk {x : K} (hx) : ((âŸ¨x, hxâŸ© : ğ“ K) : K) = x := rfl

lemma mk_eq_mk (x y : K) (hx hy) : (âŸ¨x, hxâŸ© : ğ“ K) = âŸ¨y, hyâŸ© â†” x = y := by simp

@[simp] lemma mk_one : (âŸ¨1, one_mem _âŸ© : ğ“ K) = 1 :=
  rfl

@[simp] lemma mk_zero : (âŸ¨0, zero_mem _âŸ© : ğ“ K) = 0 :=
  rfl
-- TODO: these lemmas don't seem to fire?
@[simp] lemma mk_add_mk (x y : K) (hx hy) : (âŸ¨x, hxâŸ© : ğ“ K) + âŸ¨y, hyâŸ© = âŸ¨x + y, add_mem hx hyâŸ© :=
  rfl

@[simp] lemma mk_mul_mk (x y : K) (hx hy) : (âŸ¨x, hxâŸ© : ğ“ K) * âŸ¨y, hyâŸ© = âŸ¨x * y, mul_mem hx hyâŸ© :=
  rfl

@[simp] lemma mk_sub_mk (x y : K) (hx hy) : (âŸ¨x, hxâŸ© : ğ“ K) - âŸ¨y, hyâŸ© = âŸ¨x - y, sub_mem hx hyâŸ© :=
  rfl

@[simp] lemma neg_mk (x : K) (hx) : (-âŸ¨x, hxâŸ© : ğ“ K) = âŸ¨-x, neg_mem hxâŸ© :=
  rfl

/-- The ring homomorphism `(ğ“ K) â†’+* (ğ“ L)` given by restricting a ring homomorphism
  `f : K â†’+* L` to `ğ“ K`. -/
def mapRingHom {K L F : Type*} [Field K] [Field L] [FunLike F K L]
    [RingHomClass F K L] (f : F) : (ğ“ K) â†’+* (ğ“ L) where
  toFun k := âŸ¨f k.val, map_isIntegral_int f k.2âŸ©
  map_zero' := by ext; simp only [map_mk, map_zero]
  map_one' := by ext; simp only [map_mk, map_one]
  map_add' x y:= by ext; simp only [map_mk, map_add]
  map_mul' x y := by ext; simp only [map_mk, map_mul]

/-- The ring isomorphism `(ğ“ K) â‰ƒ+* (ğ“ L)` given by restricting
  a ring isomorphism `e : K â‰ƒ+* L` to `ğ“ K`. -/
def mapRingEquiv {K L E : Type*} [Field K] [Field L] [EquivLike E K L]
    [RingEquivClass E K L] (e : E) : (ğ“ K) â‰ƒ+* (ğ“ L) :=
  RingEquiv.ofRingHom (mapRingHom e) (mapRingHom (e : K â‰ƒ+* L).symm)
    (RingHom.ext fun x => ext (EquivLike.right_inv e x.1))
      (RingHom.ext fun x => ext (EquivLike.left_inv e x.1))

end RingOfIntegers

/-- Given an algebra structure between two fields, this instance creates an algebra structure
between their two rings of integers. -/
instance inst_ringOfIntegersAlgebra [Algebra K L] : Algebra (ğ“ K) (ğ“ L) :=
  (RingOfIntegers.mapRingHom (algebraMap K L)).toAlgebra

-- diamond at `reducible_and_instances` https://github.com/leanprover-community/mathlib4/issues/10906
example : Algebra.id (ğ“ K) = inst_ringOfIntegersAlgebra K K := rfl

namespace RingOfIntegers

/-- The algebra homomorphism `(ğ“ K) â†’â‚[ğ“ k] (ğ“ L)` given by restricting an algebra homomorphism
  `f : K â†’â‚[k] L` to `ğ“ K`. -/
def mapAlgHom {k K L F : Type*} [Field k] [Field K] [Field L] [Algebra k K]
    [Algebra k L] [FunLike F K L] [AlgHomClass F k K L] (f : F) : (ğ“ K) â†’â‚[ğ“ k] (ğ“ L) where
  toRingHom := mapRingHom f
  commutes' x := SetCoe.ext (AlgHomClass.commutes ((f : K â†’â‚[k] L).restrictScalars (ğ“ k)) x)

/-- The isomorphism of algebras `(ğ“ K) â‰ƒâ‚[ğ“ k] (ğ“ L)` given by restricting
  an isomorphism of algebras `e : K â‰ƒâ‚[k] L` to `ğ“ K`. -/
def mapAlgEquiv {k K L E : Type*} [Field k] [Field K] [Field L] [Algebra k K]
    [Algebra k L] [EquivLike E K L] [AlgEquivClass E k K L] (e : E) : (ğ“ K) â‰ƒâ‚[ğ“ k] (ğ“ L) :=
  AlgEquiv.ofAlgHom (mapAlgHom e) (mapAlgHom (e : K â‰ƒâ‚[k] L).symm)
    (AlgHom.ext fun x => ext (EquivLike.right_inv e x.1))
      (AlgHom.ext fun x => ext (EquivLike.left_inv e x.1))

instance inst_isScalarTower (k K L : Type*) [Field k] [Field K] [Field L]
    [Algebra k K] [Algebra k L] [Algebra K L] [IsScalarTower k K L] :
    IsScalarTower (ğ“ k) (ğ“ K) (ğ“ L) :=
  IsScalarTower.of_algHom (mapAlgHom (IsScalarTower.toAlgHom k K L))

variable {K}

/-- The canonical map from `ğ“ K` to `K` is injective.

This is a convenient abbreviation for `FaithfulSMul.algebraMap_injective`.
-/
lemma coe_injective : Function.Injective (algebraMap (ğ“ K) K) :=
  FaithfulSMul.algebraMap_injective _ _

/-- The canonical map from `ğ“ K` to `K` is injective.

This is a convenient abbreviation for `map_eq_zero_iff` applied to
`FaithfulSMul.algebraMap_injective`.
-/
lemma coe_eq_zero_iff {x : ğ“ K} : algebraMap _ K x = 0 â†” x = 0 :=
  map_eq_zero_iff _ coe_injective

/-- The canonical map from `ğ“ K` to `K` is injective.

This is a convenient abbreviation for `map_ne_zero_iff` applied to
`FaithfulSMul.algebraMap_injective`.
-/
lemma coe_ne_zero_iff {x : ğ“ K} : algebraMap _ K x â‰  0 â†” x â‰  0 :=
  map_ne_zero_iff _ coe_injective

theorem isIntegral_coe (x : ğ“ K) : IsIntegral â„¤ (algebraMap _ K x) :=
  x.2

theorem isIntegral (x : ğ“ K) : IsIntegral â„¤ x := by
  obtain âŸ¨P, hPm, hPâŸ© := x.isIntegral_coe
  refine âŸ¨P, hPm, ?_âŸ©
  rwa [IsScalarTower.algebraMap_eq (S := ğ“ K), â† Polynomial.hom_evalâ‚‚, coe_eq_zero_iff] at hP

instance [NumberField K] : IsFractionRing (ğ“ K) K :=
  integralClosure.isFractionRing_of_finite_extension â„š _

instance : IsIntegralClosure (ğ“ K) â„¤ K :=
  integralClosure.isIntegralClosure _ _

instance : Algebra.IsIntegral â„¤ (ğ“ K) :=
  IsIntegralClosure.isIntegral_algebra â„¤ K

instance [NumberField K] : IsIntegrallyClosed (ğ“ K) :=
  integralClosure.isIntegrallyClosedOfFiniteExtension â„š

/-- The ring of integers of `K` are equivalent to any integral closure of `â„¤` in `K` -/
protected noncomputable def equiv (R : Type*) [CommRing R] [Algebra R K]
    [IsIntegralClosure R â„¤ K] : ğ“ K â‰ƒ+* R :=
  (IsIntegralClosure.equiv â„¤ R K _).symm.toRingEquiv

variable (K)

instance [CharZero K] : CharZero (ğ“ K) :=
  CharZero.of_module _ K

variable [NumberField K]

instance : IsNoetherian â„¤ (ğ“ K) :=
  IsIntegralClosure.isNoetherian _ â„š K _

/-- The ring of integers of a number field is not a field. -/
theorem not_isField : Â¬IsField (ğ“ K) := by
  have h_inj : Function.Injective (algebraMap â„¤ (ğ“ K)) := RingHom.injective_int (algebraMap â„¤ (ğ“ K))
  intro hf
  exact Int.not_isField
    (((IsIntegralClosure.isIntegral_algebra â„¤ K).isField_iff_isField h_inj).mpr hf)

instance : IsDedekindDomain (ğ“ K) :=
  IsIntegralClosure.isDedekindDomain â„¤ â„š K _

instance : Free â„¤ (ğ“ K) :=
  IsIntegralClosure.module_free â„¤ â„š K (ğ“ K)

instance : IsLocalization (Algebra.algebraMapSubmonoid (ğ“ K) â„¤â°) K :=
  IsIntegralClosure.isLocalization_of_isSeparable â„¤ â„š K (ğ“ K)

/-- A â„¤-basis of the ring of integers of `K`. -/
noncomputable def basis : Basis (Free.ChooseBasisIndex â„¤ (ğ“ K)) â„¤ (ğ“ K) :=
  Free.chooseBasis â„¤ (ğ“ K)

variable {K} {M : Type*}

/-- Given `f : M â†’ K` such that `âˆ€ x, IsIntegral â„¤ (f x)`, the corresponding function
`M â†’ ğ“ K`. -/
def restrict (f : M â†’ K) (h : âˆ€ x, IsIntegral â„¤ (f x)) (x : M) : ğ“ K :=
  âŸ¨f x, h xâŸ©

/-- Given `f : M â†’+ K` such that `âˆ€ x, IsIntegral â„¤ (f x)`, the corresponding function
`M â†’+ ğ“ K`. -/
def restrict_addMonoidHom [AddZeroClass M] (f : M â†’+ K) (h : âˆ€ x, IsIntegral â„¤ (f x)) :
    M â†’+ ğ“ K where
  toFun := restrict f h
  map_zero' := by simp only [restrict, map_zero, mk_zero]
  map_add' x y := by simp only [restrict, map_add, mk_add_mk _]

/-- Given `f : M â†’* K` such that `âˆ€ x, IsIntegral â„¤ (f x)`, the corresponding function
`M â†’* ğ“ K`. -/
def restrict_monoidHom [MulOneClass M] (f : M â†’* K) (h : âˆ€ x, IsIntegral â„¤ (f x)) : M â†’* ğ“ K where
  toFun := restrict f h
  map_one' := by simp only [restrict, map_one, mk_one]
  map_mul' x y := by simp only [restrict, map_mul, mk_mul_mk _]

section extension

variable (K L : Type*) [Field K] [Field L] [Algebra K L]

instance : IsScalarTower (ğ“ K) (ğ“ L) L :=
  IsScalarTower.of_algebraMap_eq' rfl

instance : IsIntegralClosure (ğ“ L) (ğ“ K) L :=
  IsIntegralClosure.tower_top (R := â„¤)

/-- The ring of integers of `L` is isomorphic to any integral closure of `ğ“ K` in `L` -/
protected noncomputable def algEquiv (R : Type*) [CommRing R] [Algebra (ğ“ K) R] [Algebra R L]
    [IsScalarTower (ğ“ K) R L] [IsIntegralClosure R (ğ“ K) L] : ğ“ L â‰ƒâ‚[ğ“ K] R :=
  (IsIntegralClosure.equiv (ğ“ K) R L _).symm

/-- Any extension between ring of integers is integral. -/
instance extension_algebra_isIntegral : Algebra.IsIntegral (ğ“ K) (ğ“ L) :=
  IsIntegralClosure.isIntegral_algebra (ğ“ K) L

/-- Any extension between ring of integers of number fields is Noetherian. -/
instance extension_isNoetherian [NumberField K] [NumberField L] : IsNoetherian (ğ“ K) (ğ“ L) :=
  IsIntegralClosure.isNoetherian (ğ“ K) K L (ğ“ L)

/-- The kernel of the algebraMap between ring of integers is `âŠ¥`. -/
theorem ker_algebraMap_eq_bot : RingHom.ker (algebraMap (ğ“ K) (ğ“ L)) = âŠ¥ :=
  (RingHom.ker_eq_bot_iff_eq_zero (algebraMap (ğ“ K) (ğ“ L))).mpr <| fun x hx => by
  have h : (algebraMap K L) x = (algebraMap (ğ“ K) (ğ“ L)) x := rfl
  simp only [hx, map_zero, map_eq_zero, RingOfIntegers.coe_eq_zero_iff] at h
  exact h

/-- The algebraMap between ring of integers is injective. -/
theorem algebraMap.injective : Function.Injective (algebraMap (ğ“ K) (ğ“ L)) :=
  (RingHom.injective_iff_ker_eq_bot (algebraMap (ğ“ K) (ğ“ L))).mpr (ker_algebraMap_eq_bot K L)

instance : NoZeroSMulDivisors (ğ“ K) (ğ“ L) :=
  NoZeroSMulDivisors.iff_algebraMap_injective.mpr <| algebraMap.injective K L

instance : NoZeroSMulDivisors (ğ“ K) L :=
  NoZeroSMulDivisors.trans_faithfulSMul (ğ“ K) (ğ“ L) L

end extension

end RingOfIntegers

variable [NumberField K]

/-- A basis of `K` over `â„š` that is also a basis of `ğ“ K` over `â„¤`. -/
noncomputable def integralBasis : Basis (Free.ChooseBasisIndex â„¤ (ğ“ K)) â„š K :=
  Basis.localizationLocalization â„š (nonZeroDivisors â„¤) K (RingOfIntegers.basis K)

@[simp]
theorem integralBasis_apply (i : Free.ChooseBasisIndex â„¤ (ğ“ K)) :
    integralBasis K i = algebraMap (ğ“ K) K (RingOfIntegers.basis K i) :=
  Basis.localizationLocalization_apply â„š (nonZeroDivisors â„¤) K (RingOfIntegers.basis K) i

@[simp]
theorem integralBasis_repr_apply (x : (ğ“ K)) (i : Free.ChooseBasisIndex â„¤ (ğ“ K)) :
    (integralBasis K).repr (algebraMap _ _ x) i =
      (algebraMap â„¤ â„š) ((RingOfIntegers.basis K).repr x i) :=
  Basis.localizationLocalization_repr_algebraMap â„š (nonZeroDivisors â„¤) K _ x i

theorem mem_span_integralBasis {x : K} :
    x âˆˆ Submodule.span â„¤ (Set.range (integralBasis K)) â†” x âˆˆ (algebraMap (ğ“ K) K).range := by
  rw [integralBasis, Basis.localizationLocalization_span, LinearMap.mem_range,
      IsScalarTower.coe_toAlgHom', RingHom.mem_range]

theorem RingOfIntegers.rank : Module.finrank â„¤ (ğ“ K) = Module.finrank â„š K :=
  IsIntegralClosure.rank â„¤ â„š K (ğ“ K)

end NumberField

namespace Rat

open NumberField

instance numberField : NumberField â„š where
  to_charZero := inferInstance
  to_finiteDimensional := by
  -- The vector space structure of `â„š` over itself can arise in multiple ways:
  -- all fields are vector spaces over themselves (used in `Rat.finiteDimensional`)
  -- all char 0 fields have a canonical embedding of `â„š` (used in `NumberField`).
  -- Show that these coincide:
    convert (inferInstance : FiniteDimensional â„š â„š)

/-- The ring of integers of `â„š` as a number field is just `â„¤`. -/
noncomputable def ringOfIntegersEquiv : ğ“ â„š â‰ƒ+* â„¤ :=
  RingOfIntegers.equiv â„¤

@[simp]
theorem coe_ringOfIntegersEquiv (z : ğ“ â„š) :
    (Rat.ringOfIntegersEquiv z : â„š) = algebraMap (ğ“ â„š) â„š z := by
  obtain âŸ¨z, rflâŸ© := Rat.ringOfIntegersEquiv.symm.surjective z
  simp

end Rat

namespace AdjoinRoot

section

open scoped Polynomial

attribute [-instance] DivisionRing.toRatAlgebra

/-- The quotient of `â„š[X]` by the ideal generated by an irreducible polynomial of `â„š[X]`
is a number field. -/
instance {f : Polynomial â„š} [hf : Fact (Irreducible f)] : NumberField (AdjoinRoot f) where
  to_charZero := charZero_of_injective_algebraMap (algebraMap â„š _).injective
  to_finiteDimensional := by convert (AdjoinRoot.powerBasis hf.out.ne_zero).finite

end

end AdjoinRoot
