/-
Copyright (c) 2023 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.NumberTheory.NumberField.CanonicalEmbedding
import Mathlib.RingTheory.Localization.NormTrace

/-!
# Number field discriminant
This file defines the discriminant of a number field.

## Main definitions
 - `discr` the absolute discriminant of a number field.

## Tags
number field, discriminant
-/

-- TODO. Rewrite some of the FLT results on the disciminant using the definitions and results of
-- this file

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220

namespace NumberField

open Classical NumberField Matrix NumberField.InfinitePlace

variable (K : Type*) [Field K] [NumberField K]

/-- The absolute discriminant of a number field. -/
noncomputable abbrev discr : â„¤ := Algebra.discr â„¤ (RingOfIntegers.basis K)

theorem coe_discr : (discr K : â„š) = Algebra.discr â„š (integralBasis K) :=
  (Algebra.discr_localizationLocalization â„¤ _ K (RingOfIntegers.basis K)).symm

theorem discr_ne_zero : discr K â‰  0 := by
  rw [â† (Int.cast_injective (Î± := â„š)).ne_iff, coe_discr]
  exact Algebra.discr_not_zero_of_basis â„š (integralBasis K)

theorem discr_eq_discr {Î¹ : Type*} [Fintype Î¹] [DecidableEq Î¹] (b : Basis Î¹ â„¤ (ğ“ K)) :
    Algebra.discr â„¤ b = discr K := by
  let bâ‚€ := Basis.reindex (RingOfIntegers.basis K) (Basis.indexEquiv (RingOfIntegers.basis K) b)
  rw [Algebra.discr_eq_discr (ğ“ K) b bâ‚€, Basis.coe_reindex, Algebra.discr_reindex]

open MeasureTheory MeasureTheory.Measure Zspan NumberField.mixedEmbedding
  NumberField.InfinitePlace ENNReal NNReal Complex

theorem _root_.NumberField.mixedEmbedding.volume_fundamentalDomain_latticeBasis :
    volume (fundamentalDomain (latticeBasis K)) =
      (2 : â„â‰¥0âˆ)â»Â¹ ^ (NrComplexPlaces K) * sqrt â€–discr Kâ€–â‚Š := by
  let f : Module.Free.ChooseBasisIndex â„¤ (ğ“ K) â‰ƒ (K â†’+* â„‚) :=
    (canonicalEmbedding.latticeBasis K).indexEquiv (Pi.basisFun â„‚ _)
  let e : (index K) â‰ƒ Module.Free.ChooseBasisIndex â„¤ (ğ“ K) := (indexEquiv K).trans f.symm
  let M := (mixedEmbedding.stdBasis K).toMatrix ((latticeBasis K).reindex e.symm)
  let N := Algebra.embeddingsMatrixReindex â„š â„‚ (integralBasis K âˆ˜ f.symm)
    RingHom.equivRatAlgHom
  suffices M.map Complex.ofReal = (matrixToStdBasis K) *
      (Matrix.reindex (indexEquiv K).symm (indexEquiv K).symm N).transpose by
    calc volume (fundamentalDomain (latticeBasis K))
      _ = â€–((mixedEmbedding.stdBasis K).toMatrix ((latticeBasis K).reindex e.symm)).detâ€–â‚Š := by
        rw [â† fundamentalDomain_reindex _ e.symm, â† norm_toNNReal, measure_fundamentalDomain
          ((latticeBasis K).reindex e.symm), volume_fundamentalDomain_stdBasis, mul_one]
        rfl
      _ = â€–(matrixToStdBasis K).det * N.detâ€–â‚Š := by
        rw [â† nnnorm_real, â† ofReal_eq_coe, RingHom.map_det, RingHom.mapMatrix_apply, this,
          det_mul, det_transpose, det_reindex_self]
      _ = (2 : â„â‰¥0âˆ)â»Â¹ ^ Fintype.card {w : InfinitePlace K // IsComplex w} * sqrt â€–N.det ^ 2â€–â‚Š := by
        have : â€–Complex.Iâ€–â‚Š = 1 := by rw [â† norm_toNNReal, norm_eq_abs, abs_I, Real.toNNReal_one]
        rw [det_matrixToStdBasis, nnnorm_mul, nnnorm_pow, nnnorm_mul, this, mul_one, nnnorm_inv,
          coe_mul, ENNReal.coe_pow, â† norm_toNNReal, IsROrC.norm_two, Real.toNNReal_ofNat,
          coe_inv two_ne_zero, coe_ofNat, nnnorm_pow, NNReal.sqrt_sq]
      _ = (2 : â„â‰¥0âˆ)â»Â¹ ^ Fintype.card { w // IsComplex w } * NNReal.sqrt â€–discr Kâ€–â‚Š := by
        rw [â† Algebra.discr_eq_det_embeddingsMatrixReindex_pow_two, Algebra.discr_reindex,
          â† coe_discr, map_intCast, â† Complex.nnnorm_int]
  ext : 2
  dsimp only
  rw [Matrix.map_apply, Basis.toMatrix_apply, Basis.coe_reindex, Function.comp, Equiv.symm_symm,
    latticeBasis_apply, â† commMap_canonical_eq_mixed, Complex.ofReal_eq_coe,
    stdBasis_repr_eq_matrixToStdBasis_mul K _ (fun _ => rfl)]
  rfl

end NumberField

namespace Rat

open NumberField

/-- The absolute discriminant of the number field `â„š` is 1. -/
@[simp]
theorem numberField_discr : discr â„š = 1 := by
  let b : Basis (Fin 1) â„¤ (ğ“ â„š) :=
    Basis.map (Basis.singleton (Fin 1) â„¤) ringOfIntegersEquiv.toAddEquiv.toIntLinearEquiv.symm
  calc NumberField.discr â„š
    _ = Algebra.discr â„¤ b := by convert (discr_eq_discr â„š b).symm
    _ = Algebra.trace â„¤ (ğ“ â„š) (b default * b default) := by
      rw [Algebra.discr_def, Matrix.det_unique, Algebra.traceMatrix_apply, Algebra.traceForm_apply]
    _ = Algebra.trace â„¤ (ğ“ â„š) 1 := by
      rw [Basis.map_apply, RingEquiv.toAddEquiv_eq_coe, AddEquiv.toIntLinearEquiv_symm,
        AddEquiv.coe_toIntLinearEquiv, Basis.singleton_apply,
        show (AddEquiv.symm â†‘ringOfIntegersEquiv) (1 : â„¤) = ringOfIntegersEquiv.symm 1 by rfl,
        map_one, mul_one]
    _ = 1 := by rw [Algebra.trace_eq_matrix_trace b]; norm_num

alias _root_.NumberField.discr_rat := numberField_discr

end Rat
