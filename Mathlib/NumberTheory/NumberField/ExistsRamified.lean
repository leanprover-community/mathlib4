/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
module

public import Mathlib.NumberTheory.NumberField.Discriminant.Basic
public import Mathlib.NumberTheory.NumberField.Discriminant.Different
public import Mathlib.NumberTheory.RamificationInertia.Galois

/-!
# Every number field has a ramified prime over `â„š`
...except `â„š` itself.

This is a trivial corollary of `NumberField.not_dvd_discr_iff_forall_pow_mem` and
`NumberField.abs_discr_gt_two` but is placed in a separate file to avoid large imports.

-/

@[expose] public section

variable {K ğ’ª : Type*} [Field K] [NumberField K] [CommRing ğ’ª] [Algebra ğ’ª K]
variable [IsIntegralClosure ğ’ª â„¤ K]

/-- If `K` is a number field with positive rank, then there exists some maximal ideal of `ğ“ K`
that is ramified over `â„¤`. -/
lemma NumberField.exists_not_isUramifiedAt_int (H : 1 < Module.finrank â„š K) :
    âˆƒ (P : Ideal ğ’ª) (_ : P.IsMaximal), P â‰  âŠ¥ âˆ§ Â¬ Algebra.IsUnramifiedAt â„¤ P := by
  have := (IsIntegralClosure.algebraMap_injective ğ’ª â„¤ K).isDomain
  have := IsIntegralClosure.isDedekindDomain â„¤ â„š K ğ’ª
  have := CharZero.of_module ğ’ª K
  have := NumberField.abs_discr_gt_two H
  obtain âŸ¨q, hq, hqKâŸ© := Int.exists_prime_and_dvd (n := discr K) (by zify; linarith)
  have := (not_dvd_discr_iff_forall_mem (ğ’ª := ğ’ª) hq).not_right.mp hqK
  push_neg at this
  obtain âŸ¨P, hP, h, HâŸ© := this
  exact âŸ¨P, hP.isMaximal (by aesop), by aesop, HâŸ©

attribute [local simp] Ideal.span_le in
/-- If `K` is a number field with positive rank such that `K/â„š` is galois, then there exists
some rational prime `p : â„¤` such that every prime of `K` over `P` is unramified. -/
lemma NumberField.exists_not_isUramifiedAt_int_of_isGalois [IsGalois â„š K]
    (H : 1 < Module.finrank â„š K) :
    âˆƒ p : â„•, p.Prime âˆ§ âˆ€ (P : Ideal ğ’ª) (_ : P.IsPrime), â†‘p âˆˆ P â†’ Â¬ Algebra.IsUnramifiedAt â„¤ P := by
  have := (IsIntegralClosure.algebraMap_injective ğ’ª â„¤ K).isDomain
  have := IsIntegralClosure.isDedekindDomain â„¤ â„š K ğ’ª
  have := IsIntegralClosure.isFractionRing_of_finite_extension â„¤ â„š K ğ’ª
  have := IsIntegralClosure.finite â„¤ â„š K ğ’ª
  have := CharZero.of_module ğ’ª K
  obtain âŸ¨P, _, hP, hP'âŸ© := NumberField.exists_not_isUramifiedAt_int (ğ’ª := ğ’ª) H
  obtain âŸ¨p, hp : _ = Ideal.span _âŸ© := IsPrincipalIdealRing.principal (P.under â„¤)
  have hp0 : p â‰  0 := fun hp0 â†¦ hP (Ideal.eq_bot_of_comap_eq_bot (hp.trans (by aesop)))
  have : Prime p := by rw [â† Ideal.span_singleton_prime hp0, â† hp]; infer_instance
  refine âŸ¨p.natAbs, Int.prime_iff_natAbs_prime.mp this, fun Q _ hQ â†¦ ?_âŸ©
  replace hQ : (p : ğ’ª) âˆˆ Q := Q.mem_of_dvd
    (map_dvd (algebraMap _ _) p.associated_natAbs.symm.dvd) (by simpa using hQ)
  have : .span {p} = Ideal.under â„¤ Q := (Ideal.IsPrime.isMaximal (hp â–¸ inferInstance) (by simpa))
    |>.eq_of_le Ideal.IsPrime.ne_top' (by simpa)
  rwa [Algebra.isUnramifiedAt_iff_of_isDedekindDomain (by aesop),
    â† Ideal.ramificationIdxIn_eq_ramificationIdx _ _ â„š K, â† this, â† hp,
    Ideal.ramificationIdxIn_eq_ramificationIdx _ P â„š K,
    â† Algebra.isUnramifiedAt_iff_of_isDedekindDomain hP]

open IsGaloisGroup NumberField -- probably should become a namespace

theorem NumberField.supr_inertia_eq_top (K : Type*) [Field K] [NumberField K]
    (G : Type*) [Group G] [MulSemiringAction G K] [IsGaloisGroup G â„š K] :
    â¨† m : MaximalSpectrum (ğ“ K), m.asIdeal.toAddSubgroup.inertia G = âŠ¤ := by
  have : Finite G := IsGaloisGroup.finite G â„š K
  set H : Subgroup G := â¨† m : MaximalSpectrum (ğ“ K), m.asIdeal.toAddSubgroup.inertia G
  set F : IntermediateField â„š K := FixedPoints.intermediateField H
  suffices Module.finrank â„š F â‰¤ 1 by
    rw [eq_top_iff, â† fixingSubgroup_fixedPoints G â„š K H, â† le_fixedPoints_iff_le_fixingSubgroup,
      fixedPoints_top, le_bot_iff, â† IntermediateField.finrank_eq_one_iff]
    exact le_antisymm this Module.finrank_pos
  suffices h : âˆ€ (m : Ideal (ğ“ F)) (hm : m.IsMaximal), Algebra.IsUnramifiedAt â„¤ m by
    contrapose! h
    obtain âŸ¨p, h1, h2, h3âŸ© := NumberField.exists_not_isUramifiedAt_int (ğ’ª := ğ“ F) h
    exact âŸ¨p, h1, h3âŸ©
  intro m _
  have hm2 := Ideal.IsMaximal.ne_bot_of_isIntegral_int m
  rw [Algebra.isUnramifiedAt_iff_of_isDedekindDomain hm2]
  obtain âŸ¨m, hm, âŸ¨rflâŸ©âŸ© := Ideal.exists_maximal_ideal_liesOver_of_isIntegral (S := ğ“ K) m
  rw [Ideal.under_under]
  have hm1 := Ideal.IsMaximal.ne_bot_of_isIntegral_int (m.under â„¤)
  have h : m.toAddSubgroup.inertia G â‰¤ H :=
    le_iSup (fun m : MaximalSpectrum (ğ“ K) â†¦ m.asIdeal.toAddSubgroup.inertia G) âŸ¨m, hmâŸ©
  replace h : Nat.card (m.toAddSubgroup.inertia H) = Nat.card (m.toAddSubgroup.inertia G) := by
    rw [â† Subgroup.map_subgroupOf_eq_of_le h, Subgroup.card_subtype,
      AddSubgroup.subgroupOf_inertia]
  let := Ideal.Quotient.field m
  let := Ideal.Quotient.field (m.under (ğ“ F))
  let := Ideal.Quotient.field (m.under â„¤)
  rw [Ideal.card_inertia_eq_ramificationIdxIn (G := H) (m.under (ğ“ F)) hm2 m,
    Ideal.card_inertia_eq_ramificationIdxIn (G := G) (m.under â„¤) hm1 m,
    Ideal.ramificationIdxIn_eq_ramificationIdx (m.under (ğ“ F)) m H,
    Ideal.ramificationIdxIn_eq_ramificationIdx (m.under â„¤) m G] at h
  have key := Ideal.ramificationIdx_algebra_tower (Ideal.map_ne_bot_of_ne_bot hm2)
    (Ideal.map_ne_bot_of_ne_bot hm1) Ideal.map_comap_le
  rwa [h, right_eq_mulâ‚€ (Ideal.IsDedekindDomain.ramificationIdx_ne_zero_of_liesOver m hm1)] at key
