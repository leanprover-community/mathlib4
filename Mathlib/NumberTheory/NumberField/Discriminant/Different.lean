/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
module

public import Mathlib.NumberTheory.NumberField.Discriminant.Basic
public import Mathlib.RingTheory.DedekindDomain.LinearDisjoint
public import Mathlib.RingTheory.Ideal.Norm.RelNorm

/-!

# (Absolute) Discriminant and Different Ideal

## Main results
- `NumberField.absNorm_differentIdeal`:
  The norm of `differentIdeal â„¤ ğ’ª` is the absolute discriminant.
- `NumberField.natAbs_discr_eq_absNorm_differentIdeal_mul_natAbs_discr_pow`:
  Formula for the absolute discriminant of `L` in terms of that of `K` in an extension `L/K`.
- `NumberField.natAbs_discr_eq_natAbs_discr_pow_mul_natAbs_discr_pow`:
  Assume that `Kâ‚` and `Kâ‚‚` are two linear disjoint number fields with coprime different ideals.
  Then, the absolute value of the discriminant of their compositum is equal to
  `|discr Kâ‚| ^ [Kâ‚‚ : â„š] * |discr Kâ‚‚| ^ [Kâ‚ : â„š]`.

-/

@[expose] public section

namespace NumberField

variable (K ğ’ª : Type*) [Field K] [NumberField K] [CommRing ğ’ª] [Algebra ğ’ª K]
variable [IsFractionRing ğ’ª K] [IsIntegralClosure ğ’ª â„¤ K] [IsDedekindDomain ğ’ª] [CharZero ğ’ª]
variable [Module.Finite â„¤ ğ’ª]

open nonZeroDivisors IntermediateField

lemma absNorm_differentIdeal : (differentIdeal â„¤ ğ’ª).absNorm = (discr K).natAbs := by
  refine (differentIdeal â„¤ ğ’ª).toAddSubgroup.relIndex_top_right.symm.trans ?_
  rw [â† Submodule.comap_map_eq_of_injective (f := Algebra.linearMap ğ’ª K)
    (FaithfulSMul.algebraMap_injective ğ’ª K) (differentIdeal â„¤ ğ’ª)]
  refine (AddSubgroup.relIndex_comap (IsLocalization.coeSubmodule K
    (differentIdeal â„¤ ğ’ª)).toAddSubgroup (algebraMap ğ’ª K).toAddMonoidHom âŠ¤).trans ?_
  have := FractionalIdeal.quotientEquiv (R := ğ’ª) (K := K) 1 (differentIdeal â„¤ ğ’ª)
    (differentIdeal â„¤ ğ’ª)â»Â¹ 1 (by simp [differentIdeal_ne_bot]) FractionalIdeal.coeIdeal_le_one
    (le_inv_of_le_invâ‚€ (by simp [pos_iff_ne_zero, differentIdeal_ne_bot])
      (by simpa using FractionalIdeal.coeIdeal_le_one)) one_ne_zero one_ne_zero
  have := Nat.card_congr this.toEquiv
  refine this.trans ?_
  rw [FractionalIdeal.coe_one, coeIdeal_differentIdeal (K := â„š), inv_inv]
  let b := integralBasis K
  let b' := (Algebra.traceForm â„š K).dualBasis (traceForm_nondegenerate â„š K) b
  have hb : Submodule.span â„¤ (Set.range b) = (1 : Submodule ğ’ª K).restrictScalars â„¤ := by
    ext
    let e := IsIntegralClosure.equiv â„¤ (RingOfIntegers K) K ğ’ª
    simpa [e.symm.exists_congr_left, e] using mem_span_integralBasis K
  qify
  refine (AddSubgroup.relIndex_eq_abs_det (1 : Submodule ğ’ª K).toAddSubgroup (FractionalIdeal.dual
    â„¤ â„š 1 : FractionalIdeal ğ’ªâ° K).coeToSubmodule.toAddSubgroup ?_ b b' ?_ ?_).trans ?_
  Â· rw [Submodule.toAddSubgroup_le, â† FractionalIdeal.coe_one]
    exact FractionalIdeal.one_le_dual_one â„¤ â„š (L := K) (B := ğ’ª)
  Â· apply AddSubgroup.toIntSubmodule.injective
    rw [AddSubgroup.toIntSubmodule_closure, hb, Submodule.toIntSubmodule_toAddSubgroup]
  Â· apply AddSubgroup.toIntSubmodule.injective
    rw [AddSubgroup.toIntSubmodule_closure, â† LinearMap.BilinForm.dualSubmodule_span_of_basis, hb]
    simp
  Â· simp only [Module.Basis.det_apply, discr, Algebra.discr]
    rw [â† eq_intCast (algebraMap â„¤ â„š), RingHom.map_det]
    congr! 2
    ext i j
    simp [b', Module.Basis.toMatrix_apply, mul_comm (RingOfIntegers.basis K i),
      b, integralBasis_apply, â† map_mul, Algebra.trace_localization â„¤ â„¤â°]

lemma discr_mem_differentIdeal : â†‘(discr K) âˆˆ differentIdeal â„¤ ğ’ª := by
  have := (differentIdeal â„¤ ğ’ª).absNorm_mem
  cases (discr K).natAbs_eq with
  | inl h =>
    rwa [absNorm_differentIdeal (K := K), â† Int.cast_natCast, â† h] at this
  | inr h =>
    rwa [absNorm_differentIdeal (K := K), â† Int.cast_natCast, Int.eq_neg_comm.mp h,
      Int.cast_neg, neg_mem_iff] at this

attribute [local instance] FractionRing.liftAlgebra in
theorem natAbs_discr_eq_absNorm_differentIdeal_mul_natAbs_discr_pow (L ğ’ª' : Type*) [Field L]
    [NumberField L] [CommRing ğ’ª'] [Algebra ğ’ª' L] [IsFractionRing ğ’ª' L] [IsIntegralClosure ğ’ª' â„¤ L]
    [IsDedekindDomain ğ’ª'] [CharZero ğ’ª'] [Algebra K L] [Algebra ğ’ª ğ’ª'] [Algebra ğ’ª L]
    [IsScalarTower ğ’ª K L] [IsScalarTower ğ’ª ğ’ª' L] [NoZeroSMulDivisors ğ’ª ğ’ª'] [Module.Free â„¤ ğ’ª']
    [Module.Finite â„¤ ğ’ª'] [Module.Finite ğ’ª ğ’ª'] :
    (discr L).natAbs = Ideal.absNorm (differentIdeal ğ’ª ğ’ª') *
      (discr K).natAbs ^ Module.finrank K L := by
  have := congr_arg Ideal.absNorm
    (differentIdeal_eq_differentIdeal_mul_differentIdeal â„¤ ğ’ª ğ’ª')
  rwa [absNorm_differentIdeal L, map_mul, Ideal.absNorm_algebraMap,
    absNorm_differentIdeal K, Algebra.finrank_eq_of_equiv_equiv
      (FractionRing.algEquiv ğ’ª K).toRingEquiv (FractionRing.algEquiv ğ’ª' L).toRingEquiv] at this
  ext
  exact IsFractionRing.algEquiv_commutes (FractionRing.algEquiv ğ’ª K)
    (FractionRing.algEquiv ğ’ª' L) _

variable (L : Type*) [Field L]

theorem isCoprime_differentIdeal_of_isCoprime_discr {Kâ‚ Kâ‚‚ : Type*} [Field Kâ‚]
    [NumberField Kâ‚] [Field Kâ‚‚] [NumberField Kâ‚‚] [Algebra Kâ‚ L] [Algebra Kâ‚‚ L]
    (h : IsCoprime (discr Kâ‚) (discr Kâ‚‚)) :
    IsCoprime ((differentIdeal â„¤ (ğ“ Kâ‚)).map (algebraMap (ğ“ Kâ‚) (ğ“ L)))
      ((differentIdeal â„¤ (ğ“ Kâ‚‚)).map (algebraMap (ğ“ Kâ‚‚) (ğ“ L))) := by
  obtain âŸ¨u, v, hâŸ© := h
  refine Ideal.isCoprime_iff_exists.mpr âŸ¨u * discr Kâ‚, ?_, v * discr Kâ‚‚, ?_, ?_âŸ©
  Â· apply Ideal.mul_mem_left
    rw [â† map_intCast (algebraMap (ğ“ Kâ‚) (ğ“ L))]
    exact Ideal.mem_map_of_mem (algebraMap (ğ“ Kâ‚) (ğ“ L)) <| discr_mem_differentIdeal _ _
  Â· apply Ideal.mul_mem_left
    rw [â† map_intCast (algebraMap (ğ“ Kâ‚‚) (ğ“ L))]
    exact Ideal.mem_map_of_mem (algebraMap (ğ“ Kâ‚‚) (ğ“ L)) <| discr_mem_differentIdeal _ _
  rw [â† Int.cast_mul, â† Int.cast_mul, â† Int.cast_add, h, Int.cast_one]

variable [NumberField L]

theorem discr_dvd_discr [Algebra K L] :
    discr K âˆ£ discr L := by
  suffices discr K ^ Module.finrank K L âˆ£ discr L from
    dvd_trans (dvd_pow_self _ (Nat.ne_zero_of_lt Module.finrank_pos)) this
  rw [â† Int.dvd_natAbs, natAbs_discr_eq_absNorm_differentIdeal_mul_natAbs_discr_pow K (ğ“ K) L (ğ“ L),
    Nat.cast_mul, Nat.cast_pow, â† Int.mul_sign_self, mul_pow, â† mul_assoc,
    mul_comm _ (discr K ^ _), mul_assoc]
  exact Int.dvd_mul_right _ _

/--
Let `Kâ‚` and `Kâ‚‚` be two number fields and assume that `Kâ‚/â„š` is Galois. If `discr Kâ‚` and
`discr Kâ‚‚` are coprime, then they are linear disjoint over `â„š`.
-/
theorem linearDisjoint_of_isGalois_isCoprime_discr (Kâ‚ Kâ‚‚ : IntermediateField â„š L) [IsGalois â„š Kâ‚]
    (h : IsCoprime (discr Kâ‚) (discr Kâ‚‚)) :
    Kâ‚.LinearDisjoint Kâ‚‚ := by
  apply IntermediateField.LinearDisjoint.of_inf_eq_bot
  suffices IsUnit (discr â†¥(Kâ‚ âŠ“ Kâ‚‚)) by
    contrapose! this
    have : 1 < Module.finrank â„š â†¥(Kâ‚ âŠ“ Kâ‚‚) := by
      refine Nat.one_lt_iff_ne_zero_and_ne_one.mpr âŸ¨Module.finrank_pos.ne', ?_âŸ©
      rwa [ne_eq, â† IntermediateField.finrank_eq_one_iff] at this
    exact Int.isUnit_iff_abs_eq.not.mpr <| by linarith [abs_discr_gt_two this]
  exact h.isUnit_of_dvd' (NumberField.discr_dvd_discr _ _) (NumberField.discr_dvd_discr _ _)

open IntermediateField IsDedekindDomain

/--
Let `Kâ‚` and `Kâ‚‚` be two number fields and assume that their different ideals (over â„¤) are coprime.
Then, the absolute value of the discriminant of their compositum is equal to
`|discr Kâ‚| ^ [Kâ‚‚ : â„š] * |discr Kâ‚‚| ^ [Kâ‚ : â„š]`.
-/
theorem natAbs_discr_eq_natAbs_discr_pow_mul_natAbs_discr_pow (Kâ‚ Kâ‚‚ : IntermediateField â„š L)
    (hâ‚ : Kâ‚.LinearDisjoint Kâ‚‚) (hâ‚‚ : Kâ‚ âŠ” Kâ‚‚ = âŠ¤)
    (hâ‚ƒ : IsCoprime ((differentIdeal â„¤ (ğ“ Kâ‚)).map (algebraMap (ğ“ Kâ‚) (ğ“ L)))
      ((differentIdeal â„¤ (ğ“ Kâ‚‚)).map (algebraMap (ğ“ Kâ‚‚) (ğ“ L)))) :
    (discr L).natAbs =
      (discr Kâ‚).natAbs ^ Module.finrank â„š Kâ‚‚ * (discr Kâ‚‚).natAbs ^ Module.finrank â„š Kâ‚ := by
  let _ : Algebra (FractionRing (ğ“ Kâ‚)) (FractionRing (ğ“ L)) := FractionRing.liftAlgebra _ _
  have h_main := natAbs_discr_eq_absNorm_differentIdeal_mul_natAbs_discr_pow Kâ‚‚ (ğ“ Kâ‚‚) L (ğ“ L)
  rwa [differentIdeal_eq_map_differentIdeal â„¤ (ğ“ L) (ğ“ Kâ‚‚) (ğ“ Kâ‚) (Fâ‚ := Kâ‚‚) (Fâ‚‚ := Kâ‚)
    (by rwa [linearDisjoint_comm]) (by rwa [sup_comm]) (by rwa [isCoprime_comm]),
    Ideal.absNorm_algebraMap, absNorm_differentIdeal Kâ‚, hâ‚.finrank_right_eq_finrank hâ‚‚,
    Algebra.finrank_eq_of_equiv_equiv (FractionRing.algEquiv _ Kâ‚).toRingEquiv
    (FractionRing.algEquiv _ L).toRingEquiv, hâ‚.finrank_left_eq_finrank hâ‚‚] at h_main
  ext
  exact IsFractionRing.algEquiv_commutes (FractionRing.algEquiv (ğ“ Kâ‚) Kâ‚)
    (FractionRing.algEquiv (ğ“ L) L) _

end NumberField
