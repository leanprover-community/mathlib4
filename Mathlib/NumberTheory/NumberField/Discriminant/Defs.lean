/-
Copyright (c) 2023 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Init.Data.ULift
import Init.Data.Fin.Fold
import Init.Data.List.Nat.Pairwise
import Init.Data.List.Nat.Range
import Mathlib.NumberTheory.NumberField.Basic
import Mathlib.RingTheory.Localization.NormTrace
import Mathlib.Analysis.Normed.Field.Lemmas

/-!
# Number field discriminant
This file defines the discriminant of a number field.

## Main definitions

* `NumberField.discr`: the absolute discriminant of a number field.

## Tags
number field, discriminant
-/

open Module

-- TODO: Rewrite some of the FLT results on the discriminant using the definitions and results of
-- this file

namespace NumberField

variable (K : Type*) [Field K] [NumberField K]

/-- The absolute discriminant of a number field. -/
noncomputable abbrev discr : â„¤ := Algebra.discr â„¤ (RingOfIntegers.basis K)

theorem coe_discr : (discr K : â„š) = Algebra.discr â„š (integralBasis K) :=
  (Algebra.discr_localizationLocalization â„¤ _ K (RingOfIntegers.basis K)).symm

theorem discr_ne_zero : discr K â‰  0 := by
  rw [â† (Int.cast_injective (Î± := â„š)).ne_iff, coe_discr]
  exact Algebra.discr_not_zero_of_basis â„š (integralBasis K)

theorem discr_eq_discr {Î¹ : Type*} [Fintype Î¹] [DecidableEq Î¹] (b : Basis Î¹ â„¤ (ğ“ K)) :
    Algebra.discr â„¤ b = discr K := by
  let bâ‚€ := Basis.reindex (RingOfIntegers.basis K) (Basis.indexEquiv (RingOfIntegers.basis K) b)
  rw [Algebra.discr_eq_discr (ğ“ K) b bâ‚€, Basis.coe_reindex, Algebra.discr_reindex]

theorem discr_eq_discr_of_algEquiv {L : Type*} [Field L] [NumberField L] (f : K â‰ƒâ‚[â„š] L) :
    discr K = discr L := by
  let fâ‚€ : ğ“ K â‰ƒâ‚—[â„¤] ğ“ L := (f.restrictScalars â„¤).mapIntegralClosure.toLinearEquiv
  rw [â† Rat.intCast_inj, coe_discr, Algebra.discr_eq_discr_of_algEquiv (integralBasis K) f,
    â† discr_eq_discr L ((RingOfIntegers.basis K).map fâ‚€)]
  change _ = algebraMap â„¤ â„š _
  rw [â† Algebra.discr_localizationLocalization â„¤ (nonZeroDivisors â„¤) L]
  congr 1
  ext
  simp only [Function.comp_apply, integralBasis_apply, Basis.localizationLocalization_apply,
    Basis.map_apply]
  rfl

end NumberField

namespace Rat

open NumberField

/-- The absolute discriminant of the number field `â„š` is 1. -/
@[simp]
theorem numberField_discr : discr â„š = 1 := by
  let b : Basis (Fin 1) â„¤ (ğ“ â„š) :=
    Basis.map (Basis.singleton (Fin 1) â„¤) ringOfIntegersEquiv.toAddEquiv.toIntLinearEquiv.symm
  calc NumberField.discr â„š
    _ = Algebra.discr â„¤ b := by convert (discr_eq_discr â„š b).symm
    _ = Algebra.trace â„¤ (ğ“ â„š) (b default * b default) := by
      rw [Algebra.discr_def, Matrix.det_unique, Algebra.traceMatrix_apply, Algebra.traceForm_apply]
    _ = Algebra.trace â„¤ (ğ“ â„š) 1 := by
      rw [Basis.map_apply, RingEquiv.toAddEquiv_eq_coe, â† AddEquiv.toIntLinearEquiv_symm,
        AddEquiv.coe_toIntLinearEquiv, Basis.singleton_apply,
        show (AddEquiv.symm â†‘ringOfIntegersEquiv) (1 : â„¤) = ringOfIntegersEquiv.symm 1 by rfl,
        map_one, mul_one]
    _ = 1 := by rw [Algebra.trace_eq_matrix_trace b]; norm_num

alias _root_.NumberField.discr_rat := numberField_discr

end Rat

variable {Î¹ Î¹'} (K) [Field K] [DecidableEq Î¹] [DecidableEq Î¹'] [Fintype Î¹] [Fintype Î¹']

/-- If `b` and `b'` are `â„š`-bases of a number field `K` such that
`âˆ€ i j, IsIntegral â„¤ (b.toMatrix b' i j)` and `âˆ€ i j, IsIntegral â„¤ (b'.toMatrix b i j)` then
`discr â„š b = discr â„š b'`. -/
theorem Algebra.discr_eq_discr_of_toMatrix_coeff_isIntegral [NumberField K]
    {b : Basis Î¹ â„š K} {b' : Basis Î¹' â„š K} (h : âˆ€ i j, IsIntegral â„¤ (b.toMatrix b' i j))
    (h' : âˆ€ i j, IsIntegral â„¤ (b'.toMatrix b i j)) : discr â„š b = discr â„š b' := by
  replace h' : âˆ€ i j, IsIntegral â„¤ (b'.toMatrix (b.reindex (b.indexEquiv b')) i j) := by
    intro i j
    convert h' i ((b.indexEquiv b').symm j)
    simp [Basis.toMatrix_apply]
  classical
  rw [â† (b.reindex (b.indexEquiv b')).toMatrix_map_vecMul b', discr_of_matrix_vecMul,
    â† one_mul (discr â„š b), Basis.coe_reindex, discr_reindex]
  congr
  have hint : IsIntegral â„¤ ((b.reindex (b.indexEquiv b')).toMatrix b').det :=
    IsIntegral.det fun i j => h _ _
  obtain âŸ¨r, hrâŸ© := IsIntegrallyClosed.isIntegral_iff.1 hint
  have hunit : IsUnit r := by
    have : IsIntegral â„¤ (b'.toMatrix (b.reindex (b.indexEquiv b'))).det :=
      IsIntegral.det fun i j => h' _ _
    obtain âŸ¨r', hr'âŸ© := IsIntegrallyClosed.isIntegral_iff.1 this
    refine isUnit_iff_exists_inv.2 âŸ¨r', ?_âŸ©
    suffices algebraMap â„¤ â„š (r * r') = 1 by
      rw [â† RingHom.map_one (algebraMap â„¤ â„š)] at this
      exact (IsFractionRing.injective â„¤ â„š) this
    rw [RingHom.map_mul, hr, hr', â† Matrix.det_mul,
      Basis.toMatrix_mul_toMatrix_flip, Matrix.det_one]
  rw [â† RingHom.map_one (algebraMap â„¤ â„š), â† hr]
  rcases Int.isUnit_iff.1 hunit with hp | hm
  Â· simp [hp]
  Â· simp [hm]
