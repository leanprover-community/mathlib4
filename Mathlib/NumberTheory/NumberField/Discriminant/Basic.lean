/-
Copyright (c) 2023 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.Algebra.Module.ZLattice.Covolume
import Mathlib.Analysis.Real.Pi.Bounds
import Mathlib.NumberTheory.NumberField.CanonicalEmbedding.ConvexBody
import Mathlib.NumberTheory.NumberField.Discriminant.Defs
import Mathlib.NumberTheory.NumberField.InfinitePlace.TotallyRealComplex
import Mathlib.Tactic.Rify

/-!
# Number field discriminant
This file defines the discriminant of a number field.

## Main result

* `NumberField.abs_discr_gt_two`: **Hermite-Minkowski Theorem**. A nontrivial number field has
  discriminant greater than `2`.

* `NumberField.finite_of_discr_bdd`: **Hermite Theorem**. Let `N` be an integer. There are only
  finitely many number fields (in some fixed extension of `‚Ñö`) of discriminant bounded by `N`.

## Tags
number field, discriminant
-/

-- TODO. Rewrite some of the FLT results on the discriminant using the definitions and results of
-- this file

namespace NumberField

open Module NumberField NumberField.InfinitePlace Matrix

open scoped Real nonZeroDivisors

variable (K : Type*) [Field K] [NumberField K]

open MeasureTheory MeasureTheory.Measure ZSpan NumberField.mixedEmbedding
  NumberField.InfinitePlace ENNReal NNReal Complex

open scoped Classical in
theorem _root_.NumberField.mixedEmbedding.volume_fundamentalDomain_latticeBasis :
    volume (fundamentalDomain (latticeBasis K)) =
      (2 : ‚Ñù‚â•0‚àû)‚Åª¬π ^ nrComplexPlaces K * sqrt ‚Äñdiscr K‚Äñ‚Çä := by
  let f : Module.Free.ChooseBasisIndex ‚Ñ§ (ùìû K) ‚âÉ (K ‚Üí+* ‚ÑÇ) :=
    (canonicalEmbedding.latticeBasis K).indexEquiv (Pi.basisFun ‚ÑÇ _)
  let e : (index K) ‚âÉ Module.Free.ChooseBasisIndex ‚Ñ§ (ùìû K) := (indexEquiv K).trans f.symm
  let M := (mixedEmbedding.stdBasis K).toMatrix ((latticeBasis K).reindex e.symm)
  let N := Algebra.embeddingsMatrixReindex ‚Ñö ‚ÑÇ (integralBasis K ‚àò f.symm)
    RingHom.equivRatAlgHom
  suffices M.map ofRealHom = matrixToStdBasis K *
      (Matrix.reindex (indexEquiv K).symm (indexEquiv K).symm N).transpose by
    calc volume (fundamentalDomain (latticeBasis K))
      _ = ‚Äñ((mixedEmbedding.stdBasis K).toMatrix ((latticeBasis K).reindex e.symm)).det‚Äñ‚Çä := by
        rw [‚Üê fundamentalDomain_reindex _ e.symm, ‚Üê norm_toNNReal, measure_fundamentalDomain
          ((latticeBasis K).reindex e.symm), volume_fundamentalDomain_stdBasis, mul_one]
        rfl
      _ = ‚Äñ(matrixToStdBasis K).det * N.det‚Äñ‚Çä := by
        rw [‚Üê nnnorm_real, ‚Üê ofRealHom_eq_coe, RingHom.map_det, RingHom.mapMatrix_apply, this,
          det_mul, det_transpose, det_reindex_self]
      _ = (2 : ‚Ñù‚â•0‚àû)‚Åª¬π ^ Fintype.card {w : InfinitePlace K // IsComplex w} * sqrt ‚ÄñN.det ^ 2‚Äñ‚Çä := by
        have : ‚ÄñComplex.I‚Äñ‚Çä = 1 := by rw [‚Üê norm_toNNReal, norm_I, Real.toNNReal_one]
        rw [det_matrixToStdBasis, nnnorm_mul, nnnorm_pow, nnnorm_mul, this, mul_one, nnnorm_inv,
          coe_mul, ENNReal.coe_pow, ‚Üê norm_toNNReal, RCLike.norm_two, Real.toNNReal_ofNat,
          coe_inv two_ne_zero, coe_ofNat, nnnorm_pow, NNReal.sqrt_sq]
      _ = (2 : ‚Ñù‚â•0‚àû)‚Åª¬π ^ Fintype.card { w // IsComplex w } * NNReal.sqrt ‚Äñdiscr K‚Äñ‚Çä := by
        rw [‚Üê Algebra.discr_eq_det_embeddingsMatrixReindex_pow_two, Algebra.discr_reindex,
          ‚Üê coe_discr, map_intCast, ‚Üê Complex.nnnorm_intCast]
  ext : 2
  dsimp only [M]
  rw [Matrix.map_apply, Basis.toMatrix_apply, Basis.coe_reindex, Function.comp_apply,
    Equiv.symm_symm, latticeBasis_apply, ‚Üê commMap_canonical_eq_mixed, Complex.ofRealHom_eq_coe,
    stdBasis_repr_eq_matrixToStdBasis_mul K _ (fun _ => rfl)]
  rfl

open scoped Classical in
theorem _root_.NumberField.mixedEmbedding.covolume_integerLattice :
    ZLattice.covolume (mixedEmbedding.integerLattice K) =
      (2‚Åª¬π) ^ nrComplexPlaces K * ‚àö|discr K| := by
  rw [ZLattice.covolume_eq_measure_fundamentalDomain _ _ (fundamentalDomain_integerLattice K),
    measureReal_def,
    volume_fundamentalDomain_latticeBasis, ENNReal.toReal_mul, ENNReal.toReal_pow,
    ENNReal.toReal_inv, toReal_ofNat, ENNReal.coe_toReal, Real.coe_sqrt, coe_nnnorm,
    Int.norm_eq_abs]

open scoped Classical in
theorem _root_.NumberField.mixedEmbedding.covolume_idealLattice (I : (FractionalIdeal (ùìû K)‚Å∞ K)À£) :
    ZLattice.covolume (mixedEmbedding.idealLattice K I) =
      (FractionalIdeal.absNorm (I : FractionalIdeal (ùìû K)‚Å∞ K)) *
        (2‚Åª¬π) ^ nrComplexPlaces K * ‚àö|discr K| := by
  rw [ZLattice.covolume_eq_measure_fundamentalDomain _ _ (fundamentalDomain_idealLattice K I),
    measureReal_def,
    volume_fundamentalDomain_fractionalIdealLatticeBasis, volume_fundamentalDomain_latticeBasis,
    ENNReal.toReal_mul, ENNReal.toReal_mul, ENNReal.toReal_pow, ENNReal.toReal_inv, toReal_ofNat,
    ENNReal.coe_toReal, Real.coe_sqrt, coe_nnnorm, Int.norm_eq_abs,
    ENNReal.toReal_ofReal (Rat.cast_nonneg.mpr (FractionalIdeal.absNorm_nonneg I.val)), mul_assoc]

theorem exists_ne_zero_mem_ideal_of_norm_le_mul_sqrt_discr (I : (FractionalIdeal (ùìû K)‚Å∞ K)À£) :
    ‚àÉ a ‚àà (I : FractionalIdeal (ùìû K)‚Å∞ K), a ‚â† 0 ‚àß
      |Algebra.norm ‚Ñö (a : K)| ‚â§ FractionalIdeal.absNorm I.1 * (4 / œÄ) ^ nrComplexPlaces K *
        (finrank ‚Ñö K).factorial / (finrank ‚Ñö K) ^ (finrank ‚Ñö K) * Real.sqrt |discr K| := by
  classical
  -- The smallest possible value for `exists_ne_zero_mem_ideal_of_norm_le`
  let B := (minkowskiBound K I * (convexBodySumFactor K)‚Åª¬π).toReal ^ (1 / (finrank ‚Ñö K : ‚Ñù))
  have h_le : (minkowskiBound K I) ‚â§ volume (convexBodySum K B) := by
    refine le_of_eq ?_
    rw [convexBodySum_volume, ‚Üê ENNReal.ofReal_pow (by positivity), ‚Üê Real.rpow_natCast,
      ‚Üê Real.rpow_mul toReal_nonneg, div_mul_cancel‚ÇÄ, Real.rpow_one, ofReal_toReal, mul_comm,
      mul_assoc, ‚Üê coe_mul, inv_mul_cancel‚ÇÄ (convexBodySumFactor_ne_zero K), ENNReal.coe_one,
      mul_one]
    ¬∑ exact mul_ne_top (ne_of_lt (minkowskiBound_lt_top K I)) coe_ne_top
    ¬∑ exact (Nat.cast_ne_zero.mpr (ne_of_gt finrank_pos))
  convert exists_ne_zero_mem_ideal_of_norm_le K I h_le
  rw [div_pow B, ‚Üê Real.rpow_natCast B, ‚Üê Real.rpow_mul (by positivity), div_mul_cancel‚ÇÄ _
    (Nat.cast_ne_zero.mpr <| ne_of_gt finrank_pos), Real.rpow_one, mul_comm_div, mul_div_assoc']
  congr 1
  rw [eq_comm]
  calc
    _ = FractionalIdeal.absNorm I.1 * (2 : ‚Ñù)‚Åª¬π ^ nrComplexPlaces K * sqrt ‚Äñdiscr K‚Äñ‚Çä *
          (2 : ‚Ñù) ^ finrank ‚Ñö K * ((2 : ‚Ñù) ^ nrRealPlaces K * (œÄ / 2) ^ nrComplexPlaces K /
            (Nat.factorial (finrank ‚Ñö K)))‚Åª¬π := by
      simp_rw [minkowskiBound, convexBodySumFactor,
        volume_fundamentalDomain_fractionalIdealLatticeBasis,
        volume_fundamentalDomain_latticeBasis, toReal_mul, toReal_pow, toReal_inv, coe_toReal,
        toReal_ofNat, mixedEmbedding.finrank, mul_assoc]
      rw [ENNReal.toReal_ofReal (Rat.cast_nonneg.mpr (FractionalIdeal.absNorm_nonneg I.1))]
      simp_rw [NNReal.coe_inv, NNReal.coe_div, NNReal.coe_mul, NNReal.coe_pow, NNReal.coe_div,
        coe_real_pi, NNReal.coe_ofNat, NNReal.coe_natCast]
    _ = FractionalIdeal.absNorm I.1 * (2 : ‚Ñù) ^ (finrank ‚Ñö K - nrComplexPlaces K - nrRealPlaces K +
          nrComplexPlaces K : ‚Ñ§) * Real.sqrt ‚Äñdiscr K‚Äñ * Nat.factorial (finrank ‚Ñö K) *
            œÄ‚Åª¬π ^ (nrComplexPlaces K) := by
      simp_rw [inv_div, div_eq_mul_inv, mul_inv, ‚Üê zpow_neg_one, ‚Üê zpow_natCast, mul_zpow,
        ‚Üê zpow_mul, neg_one_mul, mul_neg_one, neg_neg, Real.coe_sqrt, coe_nnnorm, sub_eq_add_neg,
        zpow_add‚ÇÄ (two_ne_zero : (2 : ‚Ñù) ‚â† 0)]
      ring
    _ = FractionalIdeal.absNorm I.1 * (2 : ‚Ñù) ^ (2 * nrComplexPlaces K : ‚Ñ§) * Real.sqrt ‚Äñdiscr K‚Äñ *
          Nat.factorial (finrank ‚Ñö K) * œÄ‚Åª¬π ^ (nrComplexPlaces K) := by
      congr
      rw [‚Üê card_add_two_mul_card_eq_rank, Nat.cast_add, Nat.cast_mul, Nat.cast_ofNat]
      ring
    _ = FractionalIdeal.absNorm I.1 * (4 / œÄ) ^ nrComplexPlaces K * (finrank ‚Ñö K).factorial *
          Real.sqrt |discr K| := by
      rw [Int.norm_eq_abs, zpow_mul, show (2 : ‚Ñù) ^ (2 : ‚Ñ§) = 4 by norm_cast, div_pow,
        inv_eq_one_div, div_pow, one_pow, zpow_natCast]
      ring

theorem exists_ne_zero_mem_ringOfIntegers_of_norm_le_mul_sqrt_discr :
    ‚àÉ (a : ùìû K), a ‚â† 0 ‚àß
      |Algebra.norm ‚Ñö (a : K)| ‚â§ (4 / œÄ) ^ nrComplexPlaces K *
        (finrank ‚Ñö K).factorial / (finrank ‚Ñö K) ^ (finrank ‚Ñö K) * Real.sqrt |discr K| := by
  obtain ‚ü®_, h_mem, h_nz, h_nm‚ü© := exists_ne_zero_mem_ideal_of_norm_le_mul_sqrt_discr K ‚Üë1
  obtain ‚ü®a, rfl‚ü© := (FractionalIdeal.mem_one_iff _).mp h_mem
  refine ‚ü®a, ne_zero_of_map h_nz, ?_‚ü©
  simp_rw [Units.val_one, FractionalIdeal.absNorm_one, Rat.cast_one, one_mul] at h_nm
  exact h_nm

/--
The Minkowski lower bound `n^{2n}/((4/pi)^{2r_2}*n!^2)` for the absolute value of the discriminant
of a number field of degree n.
-/
theorem abs_discr_ge' :
    (finrank ‚Ñö K) ^ (2 * finrank ‚Ñö K) / ((4 / œÄ) ^ (2 * nrComplexPlaces K) *
      (finrank ‚Ñö K).factorial ^ 2) ‚â§ |discr K| := by
  -- We use `exists_ne_zero_mem_ringOfIntegers_of_norm_le_mul_sqrt_discr` to get a nonzero
  -- algebraic integer `x` of small norm and the fact that `1 ‚â§ |Norm x|` to get a lower bound
  -- on `sqrt |discr K|`.
  obtain ‚ü®x, h_nz, h_bd‚ü© := exists_ne_zero_mem_ringOfIntegers_of_norm_le_mul_sqrt_discr K
  have h_nm : (1 : ‚Ñù) ‚â§ |Algebra.norm ‚Ñö (x : K)| := by
    rw [‚Üê Algebra.coe_norm_int, ‚Üê Int.cast_one, ‚Üê Int.cast_abs, Rat.cast_intCast, Int.cast_le]
    exact Int.one_le_abs (Algebra.norm_ne_zero_iff.mpr h_nz)
  replace h_bd := le_trans h_nm h_bd
  rwa [‚Üê inv_mul_le_iff‚ÇÄ, inv_div, mul_one, Real.le_sqrt (by positivity) (by positivity),
    ‚Üê Int.cast_abs, div_pow, mul_pow, ‚Üê pow_mul, mul_comm _ 2, ‚Üê pow_mul, mul_comm _ 2] at h_bd
  exact div_pos (by positivity) <| pow_pos (Nat.cast_pos.mpr finrank_pos) (finrank ‚Ñö K)

theorem abs_discr_ge_of_isTotallyComplex [IsTotallyComplex K] :
    (finrank ‚Ñö K) ^ (2 * finrank ‚Ñö K) / ((4 / œÄ) ^ (finrank ‚Ñö K) *
      (finrank ‚Ñö K).factorial ^ 2) ‚â§ |discr K| := by
  have := abs_discr_ge' K
  rwa [‚Üê IsTotallyComplex.finrank] at this

theorem abs_discr_rpow_ge_of_isTotallyComplex [IsTotallyComplex K] :
    (finrank ‚Ñö K) ^ 2 / ((4 / œÄ) * (finrank ‚Ñö K).factorial ^ (2 * (finrank ‚Ñö K : ‚Ñù)‚Åª¬π)) ‚â§
        |discr K| ^ (finrank ‚Ñö K : ‚Ñù)‚Åª¬π := by
  have h : 0 < (finrank ‚Ñö K : ‚Ñù) := Nat.cast_pos.mpr finrank_pos
  rw [‚Üê Real.rpow_le_rpow_iff (z := finrank ‚Ñö K) (by positivity) (by positivity) h, Real.div_rpow
    (by positivity) (by positivity), ‚Üê Real.rpow_mul (by positivity), inv_mul_cancel‚ÇÄ h.ne',
    Real.rpow_one, Real.mul_rpow (by positivity) (by positivity), Real.rpow_natCast,
    Real.rpow_natCast, ‚Üê pow_mul, ‚Üê Real.rpow_mul (by positivity),
    inv_mul_cancel_right‚ÇÄ h.ne', Real.rpow_two]
  exact abs_discr_ge_of_isTotallyComplex K

variable {K}

theorem abs_discr_ge (h : 1 < finrank ‚Ñö K) :
    (4 / 9 : ‚Ñù) * (3 * œÄ / 4) ^ finrank ‚Ñö K ‚â§ |discr K| := by
  refine le_trans ?_ (abs_discr_ge' K)
  -- The sequence `a n` is a lower bound for `|discr K|`. We prove below by induction an uniform
  -- lower bound for this sequence from which we deduce the result.
  rw [mul_comm 2 _]
  let a : ‚Ñï ‚Üí ‚Ñù := fun n => (n : ‚Ñù) ^ (n * 2) / ((4 / œÄ) ^ n * (n.factorial : ‚Ñù) ^ 2)
  suffices ‚àÄ n, 2 ‚â§ n ‚Üí (4 / 9 : ‚Ñù) * (3 * œÄ / 4) ^ n ‚â§ a n by
    refine le_trans (this (finrank ‚Ñö K) h) ?_
    simp only [a]
    gcongr
    ¬∑ exact (one_le_div Real.pi_pos).2 Real.pi_le_four
    ¬∑ rw [‚Üê card_add_two_mul_card_eq_rank, mul_comm]
      exact Nat.le_add_left _ _
  intro n hn
  induction n, hn using Nat.le_induction with
  | base => exact le_of_eq <| by simp [a, Nat.factorial_two]; field_simp; ring
  | succ m _ h_m =>
      suffices (3 : ‚Ñù) ‚â§ (1 + 1 / m : ‚Ñù) ^ (2 * m) by
        convert_to _ ‚â§ (a m) * (1 + 1 / m : ‚Ñù) ^ (2 * m) / (4 / œÄ)
        ¬∑ simp_rw [a, add_mul, one_mul, pow_succ, Nat.factorial_succ]
          field_simp
          simp [field, div_pow]
          ring
        ¬∑ rw [_root_.le_div_iff‚ÇÄ (by positivity), pow_succ]
          convert (mul_le_mul h_m this (by positivity) (by positivity)) using 1
          field_simp
      refine le_trans (le_of_eq (by simp [field]; norm_num)) (one_add_mul_le_pow ?_ (2 * m))
      exact le_trans (by norm_num : (-2 : ‚Ñù) ‚â§ 0) (by positivity)

/-- **Hermite-Minkowski Theorem**. A nontrivial number field has discriminant greater than `2`. -/
theorem abs_discr_gt_two (h : 1 < finrank ‚Ñö K) : 2 < |discr K| := by
  rw [‚Üê Nat.succ_le_iff] at h
  rify
  calc
    (2 : ‚Ñù) < (4 / 9) * (3 * œÄ / 4) ^ 2 := by
      nlinarith [Real.pi_gt_three]
    _ ‚â§ (4 / 9 : ‚Ñù) * (3 * œÄ / 4) ^ finrank ‚Ñö K := by
      gcongr
      linarith [Real.pi_gt_three]
    _ ‚â§ |(discr K : ‚Ñù)| := mod_cast abs_discr_ge h

/-!
### Hermite Theorem
This section is devoted to the proof of Hermite theorem.

Let `N` be an integer . We prove that the set `S` of finite extensions `K` of `‚Ñö`
(in some fixed extension `A` of `‚Ñö`) such that `|discr K| ‚â§ N` is finite by proving, using
`finite_of_finite_generating_set`, that there exists a finite set `T ‚äÜ A` such that
`‚àÄ K ‚àà S, ‚àÉ x ‚àà T, K = ‚Ñö‚üÆx‚üØ` .

To find the set `T`, we construct a finite set `T‚ÇÄ` of polynomials in `‚Ñ§[X]` containing, for each
`K ‚àà S`, the minimal polynomial of a primitive element of `K`. The set `T` is then the union of
roots in `A` of the polynomials in `T‚ÇÄ`. More precisely, the set `T‚ÇÄ` is the set of all polynomials
in `‚Ñ§[X]` of degrees and coefficients bounded by some explicit constants depending only on `N`.

Indeed, we prove that, for any field `K` in `S`, its degree is bounded, see
`rank_le_rankOfDiscrBdd`, and also its Minkowski bound, see `minkowskiBound_lt_boundOfDiscBdd`.
Thus it follows from `mixedEmbedding.exists_primitive_element_lt_of_isComplex` and
`mixedEmbedding.exists_primitive_element_lt_of_isReal` that there exists an algebraic integer
`x` of `K` such that `K = ‚Ñö(x)` and the conjugates of `x` are all bounded by some quantity
depending only on `N`.

Since the primitive element `x` is constructed differently depending on whether `K` has a infinite
real place or not, the theorem is proved in two parts.
-/

namespace hermiteTheorem

open Polynomial

open scoped IntermediateField

variable (A : Type*) [Field A] [CharZero A]

theorem finite_of_finite_generating_set {p : IntermediateField ‚Ñö A ‚Üí Prop}
    (S : Set {F : IntermediateField ‚Ñö A // p F}) {T : Set A}
    (hT : T.Finite) (h : ‚àÄ F ‚àà S, ‚àÉ x ‚àà T, F = ‚Ñö‚üÆx‚üØ) :
    S.Finite := by
  rw [‚Üê Set.finite_coe_iff] at hT
  refine Set.finite_coe_iff.mp <| Finite.of_injective
    (fun ‚ü®F, hF‚ü© ‚Ü¶ (‚ü®(h F hF).choose, (h F hF).choose_spec.1‚ü© : T)) (fun _ _ h_eq ‚Ü¶ ?_)
  rw [Subtype.ext_iff, Subtype.ext_iff]
  convert congr_arg (‚Ñö‚üÆ¬∑‚üØ) (Subtype.mk_eq_mk.mp h_eq)
  all_goals exact (h _ (Subtype.mem _)).choose_spec.2

variable (N : ‚Ñï)

/-- An upper bound on the degree of a number field `K` with `|discr K| ‚â§ N`,
see `rank_le_rankOfDiscrBdd`. -/
noncomputable abbrev rankOfDiscrBdd : ‚Ñï :=
  max 1 (Nat.floor ((Real.log ((9 / 4 : ‚Ñù) * N) / Real.log (3 * œÄ / 4))))

/-- An upper bound on the Minkowski bound of a number field `K` with `|discr K| ‚â§ N`;
see `minkowskiBound_lt_boundOfDiscBdd`. -/
noncomputable abbrev boundOfDiscBdd : ‚Ñù‚â•0 := sqrt N * (2 : ‚Ñù‚â•0) ^ rankOfDiscrBdd N + 1

variable {N} (hK : |discr K| ‚â§ N)

include hK in
/-- If `|discr K| ‚â§ N` then the degree of `K` is at most `rankOfDiscrBdd`. -/
theorem rank_le_rankOfDiscrBdd :
    finrank ‚Ñö K ‚â§ rankOfDiscrBdd N := by
  have h_nz : N ‚â† 0 := by
    refine fun h ‚Ü¶ discr_ne_zero K ?_
    rwa [h, Nat.cast_zero, abs_nonpos_iff] at hK
  have h‚ÇÇ : 1 < 3 * œÄ / 4 := by
    rw [_root_.lt_div_iff‚ÇÄ (by positivity), ‚Üê _root_.div_lt_iff‚ÇÄ' (by positivity), one_mul]
    linarith [Real.pi_gt_three]
  obtain h | h := lt_or_ge 1 (finrank ‚Ñö K)
  ¬∑ apply le_max_of_le_right
    rw [Nat.le_floor_iff]
    ¬∑ have h := le_trans (abs_discr_ge h) (Int.cast_le.mpr hK)
      contrapose! h
      rw [‚Üê Real.rpow_natCast]
      rw [Real.log_div_log] at h
      refine lt_of_le_of_lt ?_ (mul_lt_mul_of_pos_left
        (Real.rpow_lt_rpow_of_exponent_lt h‚ÇÇ h) (by positivity : (0 : ‚Ñù) < 4 / 9))
      rw [Real.rpow_logb (lt_trans zero_lt_one h‚ÇÇ) (ne_of_gt h‚ÇÇ) (by positivity), ‚Üê mul_assoc,
            ‚Üê inv_div, inv_mul_cancel‚ÇÄ (by norm_num), one_mul, Int.cast_natCast]
    ¬∑ refine div_nonneg (Real.log_nonneg ?_) (Real.log_nonneg (le_of_lt h‚ÇÇ))
      rw [mul_comm, ‚Üê mul_div_assoc, _root_.le_div_iff‚ÇÄ (by positivity), one_mul,
        ‚Üê _root_.div_le_iff‚ÇÄ (by positivity)]
      exact le_trans (by norm_num) (Nat.one_le_cast.mpr (Nat.one_le_iff_ne_zero.mpr h_nz))
  ¬∑ exact le_max_of_le_left h

include hK in
/-- If `|discr K| ‚â§ N` then the Minkowski bound of `K` is less than `boundOfDiscrBdd`. -/
theorem minkowskiBound_lt_boundOfDiscBdd : minkowskiBound K ‚Üë1 < boundOfDiscBdd N := by
  have : boundOfDiscBdd N - 1 < boundOfDiscBdd N := by
    simp_rw [boundOfDiscBdd, add_tsub_cancel_right, lt_add_iff_pos_right, zero_lt_one]
  refine lt_of_le_of_lt ?_ (coe_lt_coe.mpr this)
  rw [minkowskiBound, volume_fundamentalDomain_fractionalIdealLatticeBasis, boundOfDiscBdd,
    add_tsub_cancel_right, Units.val_one, FractionalIdeal.absNorm_one, Rat.cast_one,
    ENNReal.ofReal_one, one_mul, mixedEmbedding.finrank, volume_fundamentalDomain_latticeBasis,
    coe_mul, ENNReal.coe_pow, coe_ofNat, show sqrt N = (1 : ‚Ñù‚â•0‚àû) * sqrt N by rw [one_mul]]
  gcongr
  ¬∑ exact pow_le_one‚ÇÄ (by positivity) (by norm_num)
  ¬∑ rwa [‚Üê NNReal.coe_le_coe, coe_nnnorm, Int.norm_eq_abs, ‚Üê Int.cast_abs,
      NNReal.coe_natCast, ‚Üê Int.cast_natCast, Int.cast_le]
  ¬∑ exact one_le_two
  ¬∑ exact rank_le_rankOfDiscrBdd hK

include hK in
theorem natDegree_le_rankOfDiscrBdd (a : ùìû K) (h : ‚Ñö‚üÆ(a : K)‚üØ = ‚ä§) :
    natDegree (minpoly ‚Ñ§ (a : K)) ‚â§ rankOfDiscrBdd N := by
  rw [Field.primitive_element_iff_minpoly_natDegree_eq,
    minpoly.isIntegrallyClosed_eq_field_fractions' ‚Ñö a.isIntegral_coe,
    (minpoly.monic a.isIntegral_coe).natDegree_map] at h
  exact h.symm ‚ñ∏ rank_le_rankOfDiscrBdd hK

variable (N)

theorem finite_of_discr_bdd_of_isReal :
    {K : { F : IntermediateField ‚Ñö A // FiniteDimensional ‚Ñö F} |
      haveI :  NumberField K := @NumberField.mk _ _ inferInstance K.prop
      {w : InfinitePlace K | IsReal w}.Nonempty ‚àß |discr K| ‚â§ N }.Finite := by
  classical
  -- The bound on the degree of the generating polynomials
  let D := rankOfDiscrBdd N
  -- The bound on the Minkowski bound
  let B := boundOfDiscBdd N
  -- The bound on the coefficients of the generating polynomials
  let C := Nat.ceil ((max B 1) ^ D *  Nat.choose D (D / 2))
  refine finite_of_finite_generating_set A _ (bUnion_roots_finite (algebraMap ‚Ñ§ A) D
      (Set.finite_Icc (-C : ‚Ñ§) C)) (fun ‚ü®K, hK‚ÇÄ‚ü© ‚ü®hK‚ÇÅ, hK‚ÇÇ‚ü© ‚Ü¶ ?_)
  -- We now need to prove that each field is generated by an element of the union of the root set
  simp_rw [Set.mem_iUnion]
  -- this is purely an optimization
  have : CharZero K := SubsemiringClass.instCharZero K
  haveI : NumberField K := @NumberField.mk _ _ inferInstance hK‚ÇÄ
  obtain ‚ü®w‚ÇÄ, hw‚ÇÄ‚ü© := hK‚ÇÅ
  suffices minkowskiBound K ‚Üë1 < (convexBodyLTFactor K) * B by
    obtain ‚ü®x, hx‚ÇÅ, hx‚ÇÇ‚ü© := exists_primitive_element_lt_of_isReal K hw‚ÇÄ this
    have hx := x.isIntegral_coe
    refine ‚ü®x, ‚ü®‚ü®minpoly ‚Ñ§ (x : K), ‚ü®?_, fun i ‚Ü¶ ?_‚ü©, ?_‚ü©, ?_‚ü©‚ü©
    ¬∑ exact natDegree_le_rankOfDiscrBdd hK‚ÇÇ x hx‚ÇÅ
    ¬∑ rw [Set.mem_Icc, ‚Üê abs_le, ‚Üê @Int.cast_le ‚Ñù]
      refine (Eq.trans_le ?_ <| Embeddings.coeff_bdd_of_norm_le
          ((le_iff_le (x : K) _).mp (fun w ‚Ü¶ le_of_lt (hx‚ÇÇ w))) i).trans ?_
      ¬∑ rw [minpoly.isIntegrallyClosed_eq_field_fractions' ‚Ñö hx, coeff_map, eq_intCast,
          Int.norm_cast_rat, Int.norm_eq_abs, Int.cast_abs]
      ¬∑ refine le_trans ?_ (Nat.le_ceil _)
        rw [show max ‚Üë(max (B : ‚Ñù‚â•0) 1) (1 : ‚Ñù) = max (B : ‚Ñù) 1 by simp, val_eq_coe, NNReal.coe_mul,
          NNReal.coe_pow, NNReal.coe_max, NNReal.coe_one, NNReal.coe_natCast]
        gcongr
        ¬∑ exact le_max_right _ 1
        ¬∑ exact rank_le_rankOfDiscrBdd hK‚ÇÇ
        ¬∑ exact (Nat.choose_le_choose _ (rank_le_rankOfDiscrBdd hK‚ÇÇ)).trans
            (Nat.choose_le_middle _ _)
    ¬∑ refine mem_rootSet.mpr ‚ü®minpoly.ne_zero hx, ?_‚ü©
      exact (aeval_algebraMap_eq_zero_iff A (x : K) _).mpr (minpoly.aeval ‚Ñ§ (x : K))
    ¬∑ rw [‚Üê (IntermediateField.lift_injective _).eq_iff, eq_comm] at hx‚ÇÅ
      convert hx‚ÇÅ
      ¬∑ simp only [IntermediateField.lift_top]
      ¬∑ simp only [IntermediateField.lift_adjoin, Set.image_singleton]
  calc
    minkowskiBound K 1 < B := minkowskiBound_lt_boundOfDiscBdd hK‚ÇÇ
    _ = 1 * B := by rw [one_mul]
    _ ‚â§ convexBodyLTFactor K * B := by gcongr; exact mod_cast one_le_convexBodyLTFactor K

theorem finite_of_discr_bdd_of_isComplex :
    {K : { F : IntermediateField ‚Ñö A // FiniteDimensional ‚Ñö F} |
      haveI :  NumberField K := @NumberField.mk _ _ inferInstance K.prop
      {w : InfinitePlace K | IsComplex w}.Nonempty ‚àß |discr K| ‚â§ N }.Finite := by
  classical
  -- The bound on the degree of the generating polynomials
  let D := rankOfDiscrBdd N
  -- The bound on the Minkowski bound
  let B := boundOfDiscBdd N
  -- The bound on the coefficients of the generating polynomials
  let C := Nat.ceil ((max (sqrt (1 + B ^ 2)) 1) ^ D * Nat.choose D (D / 2))
  refine finite_of_finite_generating_set A _ (bUnion_roots_finite (algebraMap ‚Ñ§ A) D
      (Set.finite_Icc (-C : ‚Ñ§) C)) (fun ‚ü®K, hK‚ÇÄ‚ü© ‚ü®hK‚ÇÅ, hK‚ÇÇ‚ü© ‚Ü¶ ?_)
  -- We now need to prove that each field is generated by an element of the union of the root set
  simp_rw [Set.mem_iUnion]
  -- this is purely an optimization
  have : CharZero K := SubsemiringClass.instCharZero K
  haveI : NumberField K := @NumberField.mk _ _ inferInstance hK‚ÇÄ
  obtain ‚ü®w‚ÇÄ, hw‚ÇÄ‚ü© := hK‚ÇÅ
  suffices minkowskiBound K ‚Üë1 < (convexBodyLT'Factor K) * boundOfDiscBdd N by
    obtain ‚ü®x, hx‚ÇÅ, hx‚ÇÇ‚ü© := exists_primitive_element_lt_of_isComplex K hw‚ÇÄ this
    have hx := x.isIntegral_coe
    refine ‚ü®x, ‚ü®‚ü®minpoly ‚Ñ§ (x : K), ‚ü®?_, fun i ‚Ü¶ ?_‚ü©, ?_‚ü©, ?_‚ü©‚ü©
    ¬∑ exact natDegree_le_rankOfDiscrBdd hK‚ÇÇ x hx‚ÇÅ
    ¬∑ rw [Set.mem_Icc, ‚Üê abs_le, ‚Üê @Int.cast_le ‚Ñù]
      refine (Eq.trans_le ?_ <| Embeddings.coeff_bdd_of_norm_le
          ((le_iff_le (x : K) _).mp (fun w ‚Ü¶ le_of_lt (hx‚ÇÇ w))) i).trans ?_
      ¬∑ rw [minpoly.isIntegrallyClosed_eq_field_fractions' ‚Ñö hx, coeff_map, eq_intCast,
          Int.norm_cast_rat, Int.norm_eq_abs, Int.cast_abs]
      ¬∑ refine le_trans ?_ (Nat.le_ceil _)
        rw [val_eq_coe, NNReal.coe_mul, NNReal.coe_pow, NNReal.coe_max, NNReal.coe_one,
          Real.coe_sqrt, NNReal.coe_add 1, NNReal.coe_one, NNReal.coe_pow]
        gcongr
        ¬∑ exact le_max_right _ 1
        ¬∑ exact rank_le_rankOfDiscrBdd hK‚ÇÇ
        ¬∑ rw [NNReal.coe_natCast, Nat.cast_le]
          exact (Nat.choose_le_choose _ (rank_le_rankOfDiscrBdd hK‚ÇÇ)).trans
            (Nat.choose_le_middle _ _)
    ¬∑ refine mem_rootSet.mpr ‚ü®minpoly.ne_zero hx, ?_‚ü©
      exact (aeval_algebraMap_eq_zero_iff A (x : K) _).mpr (minpoly.aeval ‚Ñ§ (x : K))
    ¬∑ rw [‚Üê (IntermediateField.lift_injective _).eq_iff, eq_comm] at hx‚ÇÅ
      convert hx‚ÇÅ
      ¬∑ simp only [IntermediateField.lift_top]
      ¬∑ simp only [IntermediateField.lift_adjoin, Set.image_singleton]
  calc
    minkowskiBound K 1 < B := minkowskiBound_lt_boundOfDiscBdd hK‚ÇÇ
    _ = 1 * B := by rw [one_mul]
    _ ‚â§ convexBodyLT'Factor K * B := by gcongr; exact mod_cast one_le_convexBodyLT'Factor K

/-- **Hermite Theorem**. Let `N` be an integer. There are only finitely many number fields
(in some fixed extension of `‚Ñö`) of discriminant bounded by `N`. -/
theorem _root_.NumberField.finite_of_discr_bdd :
    {K : { F : IntermediateField ‚Ñö A // FiniteDimensional ‚Ñö F} |
      haveI :  NumberField K := @NumberField.mk _ _ inferInstance K.prop
      |discr K| ‚â§ N }.Finite := by
  refine Set.Finite.subset (Set.Finite.union (finite_of_discr_bdd_of_isReal A N)
    (finite_of_discr_bdd_of_isComplex A N)) ?_
  rintro ‚ü®K, hK‚ÇÄ‚ü© hK‚ÇÅ
  -- this is purely an optimization
  have : CharZero K := SubsemiringClass.instCharZero K
  haveI : NumberField K := @NumberField.mk _ _ inferInstance hK‚ÇÄ
  obtain ‚ü®w‚ÇÄ‚ü© := (inferInstance : Nonempty (InfinitePlace K))
  by_cases hw‚ÇÄ : IsReal w‚ÇÄ
  ¬∑ apply Set.mem_union_left
    exact ‚ü®‚ü®w‚ÇÄ, hw‚ÇÄ‚ü©, hK‚ÇÅ‚ü©
  ¬∑ apply Set.mem_union_right
    exact ‚ü®‚ü®w‚ÇÄ, not_isReal_iff_isComplex.mp hw‚ÇÄ‚ü©, hK‚ÇÅ‚ü©

end hermiteTheorem

end NumberField
