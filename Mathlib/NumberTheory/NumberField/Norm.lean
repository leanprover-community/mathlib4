/-
Copyright (c) 2022 Riccardo Brasca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Riccardo Brasca, Eric Rodriguez
-/
import Mathlib.NumberTheory.NumberField.Basic
import Mathlib.RingTheory.Localization.NormTrace
import Mathlib.RingTheory.Norm.Transitivity

/-!
# Norm in number fields
Given a finite extension of number fields, we define the norm morphism as a function between the
rings of integers.

## Main definitions
* `RingOfIntegers.norm K` : `Algebra.norm` as a morphism `(ğ“ L) â†’* (ğ“ K)`.
## Main results
* `RingOfIntegers.dvd_norm` : if `L/K` is a finite Galois extension of fields, then, for all
  `(x : ğ“ L)` we have that `x âˆ£ algebraMap (ğ“ K) (ğ“ L) (norm K x)`.

-/


open scoped NumberField

open Finset NumberField Algebra Module IntermediateField

section Rat

variable {K : Type*} [Field K] [NumberField K] (x : ğ“ K)

theorem Algebra.coe_norm_int : (Algebra.norm â„¤ x : â„š) = Algebra.norm â„š (x : K) :=
  (Algebra.norm_localization (R := â„¤) (Râ‚˜ := â„š) (S := ğ“ K) (Sâ‚˜ := K) (nonZeroDivisors â„¤) x).symm

theorem Algebra.coe_trace_int : (Algebra.trace â„¤ _ x : â„š) = Algebra.trace â„š K (x : K) :=
  (Algebra.trace_localization (R := â„¤) (Râ‚˜ := â„š) (S := ğ“ K) (Sâ‚˜ := K) (nonZeroDivisors â„¤) x).symm

end Rat

namespace RingOfIntegers

variable {L : Type*} (K : Type*) [Field K] [Field L] [Algebra K L] [FiniteDimensional K L]

/-- `Algebra.norm` as a morphism between the rings of integers. -/
noncomputable def norm : ğ“ L â†’* ğ“ K :=
  RingOfIntegers.restrict_monoidHom
    ((Algebra.norm K).comp (algebraMap (ğ“ L) L : (ğ“ L) â†’* L))
    fun x => isIntegral_norm K x.2

@[simp] lemma coe_norm (x : ğ“ L) : norm K x = Algebra.norm K (x : L) :=
  rfl

theorem coe_algebraMap_norm (x : ğ“ L) :
    (algebraMap (ğ“ K) (ğ“ L) (norm K x) : L) = algebraMap K L (Algebra.norm K (x : L)) :=
  rfl

theorem algebraMap_norm_algebraMap (x : ğ“ K) : algebraMap _ K (norm K (algebraMap (ğ“ K) (ğ“ L) x)) =
      Algebra.norm K (algebraMap K L (algebraMap _ _ x)) := rfl

theorem norm_algebraMap (x : ğ“ K) : norm K (algebraMap (ğ“ K) (ğ“ L) x) = x ^ finrank K L := by
  rw [RingOfIntegers.ext_iff, RingOfIntegers.coe_eq_algebraMap,
    RingOfIntegers.algebraMap_norm_algebraMap, Algebra.norm_algebraMap,
    RingOfIntegers.coe_eq_algebraMap, map_pow]

theorem isUnit_norm_of_isGalois [IsGalois K L] {x : ğ“ L} : IsUnit (norm K x) â†” IsUnit x := by
  classical
  refine âŸ¨fun hx => ?_, IsUnit.map _âŸ©
  replace hx : IsUnit (algebraMap (ğ“ K) (ğ“ L) <| norm K x) := hx.map (algebraMap (ğ“ K) <| ğ“ L)
  refine @isUnit_of_mul_isUnit_right (ğ“ L) _
    âŸ¨(univ \ {AlgEquiv.refl}).prod fun Ïƒ : L â‰ƒâ‚[K] L => Ïƒ x,
      prod_mem fun Ïƒ _ => x.2.map (Ïƒ : L â†’+* L).toIntAlgHomâŸ© _ ?_
  convert hx using 1
  ext
  convert_to ((univ \ {AlgEquiv.refl}).prod fun Ïƒ : L â‰ƒâ‚[K] L => Ïƒ x) *
    âˆ Ïƒ âˆˆ {(AlgEquiv.refl : L â‰ƒâ‚[K] L)}, Ïƒ x = _
  Â· rw [prod_singleton, AlgEquiv.coe_refl, _root_.id, RingOfIntegers.coe_eq_algebraMap, map_mul,
      RingOfIntegers.map_mk]
  Â· rw [prod_sdiff <| subset_univ _, â† norm_eq_prod_automorphisms, coe_algebraMap_norm]

/-- If `L/K` is a finite Galois extension of fields, then, for all `(x : ğ“ L)` we have that
`x âˆ£ algebraMap (ğ“ K) (ğ“ L) (norm K x)`. -/
theorem dvd_norm [IsGalois K L] (x : ğ“ L) : x âˆ£ algebraMap (ğ“ K) (ğ“ L) (norm K x) := by
  classical
  have hint :
    IsIntegral â„¤ (âˆ Ïƒ âˆˆ univ.erase (AlgEquiv.refl : L â‰ƒâ‚[K] L), Ïƒ x) :=
    IsIntegral.prod _ (fun Ïƒ _ =>
      ((RingOfIntegers.isIntegral_coe x).map Ïƒ))
  refine âŸ¨âŸ¨_, hintâŸ©, ?_âŸ©
  ext
  rw [coe_algebraMap_norm K x, norm_eq_prod_automorphisms]
  simp [â† Finset.mul_prod_erase _ _ (mem_univ AlgEquiv.refl)]

variable (F : Type*) [Field F] [Algebra K F] [FiniteDimensional K F]

theorem norm_norm [Algebra F L] [FiniteDimensional F L] [IsScalarTower K F L] (x : ğ“ L) :
    norm K (norm F x) = norm K x := by
  rw [RingOfIntegers.ext_iff, coe_norm, coe_norm, coe_norm, Algebra.norm_norm]

variable {F}

theorem isUnit_norm [CharZero K] {x : ğ“ F} : IsUnit (norm K x) â†” IsUnit x := by
  letI : Algebra K (AlgebraicClosure K) := AlgebraicClosure.instAlgebra K
  let L := normalClosure K F (AlgebraicClosure F)
  haveI : FiniteDimensional F L := FiniteDimensional.right K F L
  haveI : IsAlgClosure K (AlgebraicClosure F) :=
    IsAlgClosure.ofAlgebraic K F (AlgebraicClosure F)
  haveI : IsGalois F L := IsGalois.tower_top_of_isGalois K F L
  calc
    IsUnit (norm K x) â†” IsUnit ((norm K) x ^ finrank F L) :=
      (isUnit_pow_iff (pos_iff_ne_zero.mp finrank_pos)).symm
    _ â†” IsUnit (norm K (algebraMap (ğ“ F) (ğ“ L) x)) := by
      rw [â† norm_norm K F (algebraMap (ğ“ F) (ğ“ L) x), norm_algebraMap F _, map_pow]
    _ â†” IsUnit (algebraMap (ğ“ F) (ğ“ L) x) := isUnit_norm_of_isGalois K
    _ â†” IsUnit (norm F (algebraMap (ğ“ F) (ğ“ L) x)) := (isUnit_norm_of_isGalois F).symm
    _ â†” IsUnit (x ^ finrank F L) := (congr_arg IsUnit (norm_algebraMap F _)).to_iff
    _ â†” IsUnit x := isUnit_pow_iff (pos_iff_ne_zero.mp finrank_pos)

end RingOfIntegers
