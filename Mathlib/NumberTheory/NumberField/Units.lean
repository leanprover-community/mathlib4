/-
Copyright (c) 2023 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.LinearAlgebra.Matrix.Gershgorin
import Mathlib.NumberTheory.NumberField.CanonicalEmbedding
import Mathlib.NumberTheory.NumberField.Norm
import Mathlib.RingTheory.Ideal.Norm
import Mathlib.RingTheory.RootsOfUnity.Basic

#align_import number_theory.number_field.units from "leanprover-community/mathlib"@"00f91228655eecdcd3ac97a7fd8dbcb139fe990a"

/-!
# Units of a number field
We prove results about the group `(ùìû K)À£` of units of the ring of integers `ùìû K` of a number
field `K`.

## Main definitions

* `NumberField.Units.rank`: the unit rank of the number field `K`.

* `NumberField.Units.fundSystem`: a fundamental system of units of `K`.

* `NumberField.Units.basisModTorsion`: a `‚Ñ§`-basis of `(ùìû K)À£ ‚ß∏ (torsion K)`
as an additive `‚Ñ§`-module.

## Main results

* `NumberField.isUnit_iff_norm`: an algebraic integer `x : ùìû K` is a unit if and only if
`|norm ‚Ñö x| = 1`.

* `NumberField.Units.mem_torsion`: a unit `x : (ùìû K)À£` is torsion iff `w x = 1` for all infinite
places `w` of `K`.

* `NumberField.Units.exist_unique_eq_mul_prod`: **Dirichlet Unit Theorem**. Any unit `x` of `ùìû K`
can be written uniquely as the product of a root of unity and powers of the units of of the
fundamental system `fundSystem`.

## Tags
number field, units
 -/


open scoped NumberField

noncomputable section

open NumberField Units BigOperators

section Rat

theorem Rat.RingOfIntegers.isUnit_iff {x : ùìû ‚Ñö} : IsUnit x ‚Üî (x : ‚Ñö) = 1 ‚à® (x : ‚Ñö) = -1 := by
  simp_rw [(isUnit_map_iff (Rat.ringOfIntegersEquiv : ùìû ‚Ñö ‚Üí+* ‚Ñ§) x).symm, Int.isUnit_iff,
    RingEquiv.coe_toRingHom, RingEquiv.map_eq_one_iff, RingEquiv.map_eq_neg_one_iff, ‚Üê
    Subtype.coe_injective.eq_iff]; rfl
#align rat.ring_of_integers.is_unit_iff Rat.RingOfIntegers.isUnit_iff

theorem Algebra.coe_norm_int {K : Type*} [Field K] [NumberField K] (x : ùìû K) :
    Algebra.norm ‚Ñ§ x = Algebra.norm ‚Ñö (x : K) :=
  (Algebra.norm_localization (R := ‚Ñ§) (R‚Çò := ‚Ñö) (S := ùìû K) (S‚Çò := K) (nonZeroDivisors ‚Ñ§) x).symm

end Rat

variable (K : Type*) [Field K]

section IsUnit

variable {K}

theorem NumberField.isUnit_iff_norm [NumberField K] {x : ùìû K} :
    IsUnit x ‚Üî |(RingOfIntegers.norm ‚Ñö x : ‚Ñö)| = 1 := by
  convert (RingOfIntegers.isUnit_norm ‚Ñö (F := K)).symm
  rw [‚Üê abs_one, abs_eq_abs, ‚Üê Rat.RingOfIntegers.isUnit_iff]
#align is_unit_iff_norm NumberField.isUnit_iff_norm

end IsUnit

namespace NumberField.Units

section coe

theorem coe_injective : Function.Injective ((‚Üë) : (ùìû K)À£ ‚Üí K) :=
  fun _ _ h => by rwa [SetLike.coe_eq_coe, Units.eq_iff] at h

variable {K}

theorem coe_mul (x y : (ùìû K)À£) : ((x * y : (ùìû K)À£) : K) = (x : K) * (y : K) := rfl

theorem coe_pow (x : (ùìû K)À£) (n : ‚Ñï) : (x ^ n : K) = (x : K) ^ n := by
  rw [‚Üê SubmonoidClass.coe_pow, ‚Üê val_pow_eq_pow_val]

theorem coe_zpow (x : (ùìû K)À£) (n : ‚Ñ§) : (x ^ n : K) = (x : K) ^ n := by
  change ((Units.coeHom K).comp (map (algebraMap (ùìû K) K))) (x ^ n) = _
  exact map_zpow _ x n

theorem coe_one : ((1 : (ùìû K)À£) : K) = (1 : K) := rfl

theorem coe_neg_one : ((-1 : (ùìû K)À£) : K) = (-1 : K) := rfl

theorem coe_ne_zero (x : (ùìû K)À£) : (x : K) ‚â† 0 :=
  Subtype.coe_injective.ne_iff.mpr (_root_.Units.ne_zero x)

end coe

open NumberField.InfinitePlace

section torsion

/-- The torsion subgroup of the group of units. -/
def torsion : Subgroup (ùìû K)À£ := CommGroup.torsion (ùìû K)À£

theorem mem_torsion {x : (ùìû K)À£} [NumberField K] :
    x ‚àà torsion K ‚Üî ‚àÄ w : InfinitePlace K, w x = 1 := by
  rw [eq_iff_eq (x : K) 1, torsion, CommGroup.mem_torsion, isOfFinOrder_iff_pow_eq_one]
  refine ‚ü®fun ‚ü®n, h_pos, h_eq‚ü© œÜ => ?_, fun h => ?_‚ü©
  ¬∑ refine norm_map_one_of_pow_eq_one œÜ.toMonoidHom (k := ‚ü®n, h_pos‚ü©) ?_
    rw [PNat.mk_coe, ‚Üê coe_pow, h_eq, coe_one]
  ¬∑ obtain ‚ü®n, hn, hx‚ü© := Embeddings.pow_eq_one_of_norm_eq_one K ‚ÑÇ x.val.prop h
    exact ‚ü®n, hn, by ext; rw [coe_pow, hx, coe_one]‚ü©

/-- Shortcut instance because Lean tends to time out before finding the general instance. -/
instance : Nonempty (torsion K) := One.nonempty

/-- The torsion subgroup is finite. -/
instance [NumberField K] : Fintype (torsion K) := by
  refine @Fintype.ofFinite _ (Set.finite_coe_iff.mpr ?_)
  refine Set.Finite.of_finite_image ?_ ((coe_injective K).injOn _)
  refine (Embeddings.finite_of_norm_le K ‚ÑÇ 1).subset
    (fun a ‚ü®u, ‚ü®h_tors, h_ua‚ü©‚ü© => ‚ü®?_, fun œÜ => ?_‚ü©)
  ¬∑ rw [‚Üê h_ua]
    exact u.val.prop
  ¬∑ rw [‚Üê h_ua]
    exact le_of_eq ((eq_iff_eq _ 1).mp ((mem_torsion K).mp h_tors) œÜ)

-- a shortcut instance to stop the next instance from timing out
instance [NumberField K] : Finite (torsion K) := inferInstance

/-- The torsion subgroup is cylic. -/
instance [NumberField K] : IsCyclic (torsion K) := subgroup_units_cyclic _

/-- The order of the torsion subgroup as positive integer. -/
def torsionOrder [NumberField K] : ‚Ñï+ := ‚ü®Fintype.card (torsion K), Fintype.card_pos‚ü©

/-- If `k` does not divide `torsionOrder` then there are no nontrivial roots of unity of
  order dividing `k`. -/
theorem rootsOfUnity_eq_one [NumberField K] {k : ‚Ñï+} (hc : Nat.Coprime k (torsionOrder K))
    {Œ∂ : (ùìû K)À£} : Œ∂ ‚àà rootsOfUnity k (ùìû K) ‚Üî Œ∂ = 1 := by
  rw [mem_rootsOfUnity]
  refine ‚ü®fun h => ?_, fun h => by rw [h, one_pow]‚ü©
  refine orderOf_eq_one_iff.mp (Nat.eq_one_of_dvd_coprimes hc ?_ ?_)
  ¬∑ exact orderOf_dvd_of_pow_eq_one h
  ¬∑ have hŒ∂ : Œ∂ ‚àà torsion K := by
      rw [torsion, CommGroup.mem_torsion, isOfFinOrder_iff_pow_eq_one]
      exact ‚ü®k, k.prop, h‚ü©
    rw [orderOf_submonoid (‚ü®Œ∂, hŒ∂‚ü© : torsion K)]
    exact orderOf_dvd_card_univ

/-- The group of roots of unity of order dividing `torsionOrder` is equal to the torsion
group. -/
theorem rootsOfUnity_eq_torsion [NumberField K] :
    rootsOfUnity (torsionOrder K) (ùìû K) = torsion K := by
  ext Œ∂
  rw [torsion, mem_rootsOfUnity]
  refine ‚ü®fun h => ?_, fun h => ?_‚ü©
  ¬∑ rw [CommGroup.mem_torsion, isOfFinOrder_iff_pow_eq_one]
    exact ‚ü®‚Üë(torsionOrder K), (torsionOrder K).prop, h‚ü©
  ¬∑ exact Subtype.ext_iff.mp (@pow_card_eq_one (torsion K) _ ‚ü®Œ∂, h‚ü© _)

end torsion

namespace dirichletUnitTheorem

/-!
### Dirichlet Unit Theorem
This section is devoted to the proof of Dirichlet's unit theorem.

We define a group morphism from `(ùìû K)À£` to `{w : InfinitePlace K // w ‚â† w‚ÇÄ} ‚Üí ‚Ñù` where `w‚ÇÄ` is a
distinguished (arbitrary) infinite place, prove that its kernel is the torsion subgroup (see
`logEmbedding_eq_zero_iff`) and that its image, called `unitLattice`, is a full `‚Ñ§`-lattice. It
follows that `unitLattice` is a free `‚Ñ§`-module (see `unitLattice_moduleFree `) of rank
`card (InfinitePlaces K) - 1` (see `unitLattice_rank`). To prove that the `unitLattice` is a full
`‚Ñ§`-lattice, we need to prove that it is discrete (see `unitLattice_inter_ball_finite`) and that it
spans the full space over `‚Ñù` (see ` unitLattice_span_eq_top`); this is the main part of the proof,
see the section `span_top` below for more details.
-/

open Classical Finset

variable [NumberField K]

variable {K}

/-- The distinguished infinite place. -/
def w‚ÇÄ : InfinitePlace K := (inferInstance : Nonempty (InfinitePlace K)).some

variable (K)

/-- The logarithmic embedding of the units (seen as an `Additive` group). -/
def logEmbedding : Additive ((ùìû K)À£) ‚Üí+ ({w : InfinitePlace K // w ‚â† w‚ÇÄ} ‚Üí ‚Ñù) :=
{ toFun := fun x w => mult w.val * Real.log (w.val (Additive.toMul x))
  map_zero' := by simp; rfl
  map_add' := fun _ _ => by simp [Real.log_mul, mul_add]; rfl }

variable {K}

@[simp]
theorem logEmbedding_component (x : (ùìû K)À£) (w : {w : InfinitePlace K // w ‚â† w‚ÇÄ}) :
    (logEmbedding K x) w = mult w.val * Real.log (w.val x) := rfl

theorem sum_logEmbedding_component (x : (ùìû K)À£) :
    ‚àë w, logEmbedding K x w = - mult (w‚ÇÄ : InfinitePlace K) * Real.log (w‚ÇÄ (x : K)) := by
  have h := congr_arg Real.log (prod_eq_abs_norm (x : K))
  rw [show |(Algebra.norm ‚Ñö) (x : K)| = 1 from isUnit_iff_norm.mp x.isUnit, Rat.cast_one,
    Real.log_one, Real.log_prod] at h
  ¬∑ simp_rw [Real.log_pow] at h
    rw [‚Üê insert_erase (mem_univ w‚ÇÄ), sum_insert (not_mem_erase w‚ÇÄ univ), add_comm,
      add_eq_zero_iff_eq_neg] at h
    convert h using 1
    ¬∑ refine (sum_subtype _ (fun w => ?_) (fun w => (mult w) * (Real.log (w (x : K))))).symm
      exact ‚ü®ne_of_mem_erase, fun h => mem_erase_of_ne_of_mem h (mem_univ w)‚ü©
    ¬∑ norm_num
  ¬∑ exact fun w _ => pow_ne_zero _ (AbsoluteValue.ne_zero _ (coe_ne_zero x))

theorem mult_log_place_eq_zero {x : (ùìû K)À£} {w : InfinitePlace K} :
    mult w * Real.log (w x) = 0 ‚Üî w x = 1 := by
  rw [mul_eq_zero, or_iff_right, Real.log_eq_zero, or_iff_right, or_iff_left]
  ¬∑ linarith [(map_nonneg _ _ : 0 ‚â§ w x)]
  ¬∑ simp only [ne_eq, map_eq_zero, coe_ne_zero x]
  ¬∑ refine (ne_of_gt ?_)
    rw [mult]; split_ifs <;> norm_num

theorem logEmbedding_eq_zero_iff {x : (ùìû K)À£} :
    logEmbedding K x = 0 ‚Üî x ‚àà torsion K := by
  rw [mem_torsion]
  refine ‚ü®fun h w => ?_, fun h => ?_‚ü©
  ¬∑ by_cases hw : w = w‚ÇÄ
    ¬∑ suffices -mult w‚ÇÄ * Real.log (w‚ÇÄ (x : K)) = 0 by
        rw [neg_mul, neg_eq_zero, ‚Üê hw] at this
        exact mult_log_place_eq_zero.mp this
      rw [‚Üê sum_logEmbedding_component, sum_eq_zero]
      exact fun w _ => congrFun h w
    ¬∑ exact mult_log_place_eq_zero.mp (congrFun h ‚ü®w, hw‚ü©)
  ¬∑ ext w
    rw [logEmbedding_component, h w.val, Real.log_one, mul_zero, Pi.zero_apply]

theorem logEmbedding_component_le {r : ‚Ñù} {x : (ùìû K)À£} (hr : 0 ‚â§ r) (h : ‚ÄñlogEmbedding K x‚Äñ ‚â§ r)
    (w : {w : InfinitePlace K // w ‚â† w‚ÇÄ}) : |logEmbedding K x w| ‚â§ r := by
  lift r to NNReal using hr
  simp_rw [Pi.norm_def, NNReal.coe_le_coe, Finset.sup_le_iff, ‚Üê NNReal.coe_le_coe] at h
  exact h w (mem_univ _)

theorem log_le_of_logEmbedding_le {r : ‚Ñù} {x : (ùìû K)À£} (hr : 0 ‚â§ r) (h : ‚ÄñlogEmbedding K x‚Äñ ‚â§ r)
    (w : InfinitePlace K) : |Real.log (w x)| ‚â§ (Fintype.card (InfinitePlace K)) * r := by
  have tool : ‚àÄ x : ‚Ñù, 0 ‚â§ x ‚Üí x ‚â§ mult w * x := fun x hx => by
      nth_rw 1 [‚Üê one_mul x]
      refine mul_le_mul ?_ le_rfl hx ?_
      all_goals { rw [mult]; split_ifs <;> norm_num }
  by_cases hw : w = w‚ÇÄ
  ¬∑ have hyp := congr_arg (‚Äñ¬∑‚Äñ) (sum_logEmbedding_component x).symm
    replace hyp := (le_of_eq hyp).trans (norm_sum_le _ _)
    simp_rw [norm_mul, norm_neg, Real.norm_eq_abs, Nat.abs_cast] at hyp
    refine (le_trans ?_ hyp).trans ?_
    ¬∑ rw [‚Üê hw]
      exact tool _ (abs_nonneg _)
    ¬∑ refine (sum_le_card_nsmul univ _  _
        (fun w _ => logEmbedding_component_le hr h w)).trans ?_
      rw [nsmul_eq_mul]
      refine mul_le_mul ?_ le_rfl hr (Fintype.card (InfinitePlace K)).cast_nonneg
      simp [card_univ]
  ¬∑ have hyp := logEmbedding_component_le hr h ‚ü®w, hw‚ü©
    rw [logEmbedding_component, abs_mul, Nat.abs_cast] at hyp
    refine (le_trans ?_ hyp).trans ?_
    ¬∑ exact tool _ (abs_nonneg _)
    ¬∑ nth_rw 1 [‚Üê one_mul r]
      exact mul_le_mul (Nat.one_le_cast.mpr Fintype.card_pos) (le_of_eq rfl) hr (Nat.cast_nonneg _)

variable (K)

/-- The lattice formed by the image of the logarithmic embedding. -/
noncomputable def _root_.NumberField.Units.unitLattice :
    AddSubgroup ({w : InfinitePlace K // w ‚â† w‚ÇÄ} ‚Üí ‚Ñù) :=
  AddSubgroup.map (logEmbedding K) ‚ä§

theorem unitLattice_inter_ball_finite (r : ‚Ñù) :
    ((unitLattice K : Set ({ w : InfinitePlace K // w ‚â† w‚ÇÄ} ‚Üí ‚Ñù)) ‚à©
      Metric.closedBall 0 r).Finite := by
  obtain hr | hr := lt_or_le r 0
  ¬∑ convert Set.finite_empty
    rw [Metric.closedBall_eq_empty.mpr hr]
    exact Set.inter_empty _
  ¬∑ suffices {x : (ùìû K)À£ | IsIntegral ‚Ñ§ (x : K) ‚àß
        ‚àÄ (œÜ : K ‚Üí+* ‚ÑÇ), ‚ÄñœÜ x‚Äñ ‚â§ Real.exp ((Fintype.card (InfinitePlace K)) * r)}.Finite by
      refine (Set.Finite.image (logEmbedding K) this).subset ?_
      rintro _ ‚ü®‚ü®x, ‚ü®_, rfl‚ü©‚ü©, hx‚ü©
      refine ‚ü®x, ‚ü®x.val.prop, (le_iff_le _ _).mp (fun w => (Real.log_le_iff_le_exp ?_).mp ?_)‚ü©, rfl‚ü©
      ¬∑ exact pos_iff.mpr (coe_ne_zero x)
      ¬∑ rw [mem_closedBall_zero_iff] at hx
        exact (le_abs_self _).trans (log_le_of_logEmbedding_le hr hx w)
    refine Set.Finite.of_finite_image ?_ ((coe_injective K).injOn _)
    refine (Embeddings.finite_of_norm_le K ‚ÑÇ
        (Real.exp ((Fintype.card (InfinitePlace K)) * r))).subset ?_
    rintro _ ‚ü®x, ‚ü®‚ü®h_int, h_le‚ü©, rfl‚ü©‚ü©
    exact ‚ü®h_int, h_le‚ü©

section span_top

/-!
#### Section `span_top`

In this section, we prove that the span over `‚Ñù` of the `unitLattice` is equal to the full space.
For this, we construct for each infinite place `w‚ÇÅ ‚â† w‚ÇÄ` a unit `u_w‚ÇÅ` of `K` such that, for all
infinite places `w` such that `w ‚â† w‚ÇÅ`, we have `Real.log w (u_w‚ÇÅ) < 0`
(and thus `Real.log w‚ÇÅ (u_w‚ÇÅ) > 0`). It follows then from a determinant computation
(using `Matrix.det_ne_zero_of_sum_col_lt_diag`) that the image by `logEmbedding` of these units is
a `‚Ñù`-linearly independent family. The unit `u_w‚ÇÅ` is obtained by constructing a sequence `seq n`
of nonzero algebraic integers that is strictly decreasing at infinite places distinct from `w‚ÇÅ` and
of norm `‚â§ B`. Since there are finitely many ideals of norm `‚â§ B`, there exists two term in the
sequence defining the same ideal and their quotient is the desired unit `u_w‚ÇÅ` (see `exists_unit`).
-/

open NumberField.mixedEmbedding NNReal

-- See: https://github.com/leanprover/lean4/issues/2220
local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y)

variable (w‚ÇÅ : InfinitePlace K) {B : ‚Ñï} (hB : minkowskiBound K < (convexBodyLtFactor K) * B)

/-- This result shows that there always exists a next term in the sequence. -/
theorem seq_next {x : ùìû K} (hx : x ‚â† 0) :
    ‚àÉ y : ùìû K, y ‚â† 0 ‚àß (‚àÄ w, w ‚â† w‚ÇÅ ‚Üí w y < w x) ‚àß |Algebra.norm ‚Ñö (y : K)| ‚â§ B := by
  let f : InfinitePlace K ‚Üí ‚Ñù‚â•0 :=
    fun w => ‚ü®(w x) / 2, div_nonneg (AbsoluteValue.nonneg _ _) (by norm_num)‚ü©
  suffices ‚àÄ w, w ‚â† w‚ÇÅ ‚Üí f w ‚â† 0 by
    obtain ‚ü®g, h_geqf, h_gprod‚ü© := adjust_f K B this
    obtain ‚ü®y, h_ynz, h_yle‚ü© := exists_ne_zero_mem_ringOfIntegers_lt (f := g)
      (by rw [convexBodyLt_volume]; convert hB; exact congr_arg ((‚Üë): NNReal ‚Üí ENNReal) h_gprod)
    refine ‚ü®y, h_ynz, fun w hw => (h_geqf w hw ‚ñ∏ h_yle w).trans ?_, ?_‚ü©
    ¬∑ rw [‚Üê Rat.cast_le (K := ‚Ñù), Rat.cast_coe_nat]
      calc
        _ = ‚àè w : InfinitePlace K, w y ^ mult w := (prod_eq_abs_norm (y : K)).symm
        _ ‚â§ ‚àè w : InfinitePlace K, (g w : ‚Ñù) ^ mult w := by
          refine prod_le_prod ?_ ?_
          ¬∑ exact fun _ _ => pow_nonneg (by positivity) _
          ¬∑ exact fun w _ => pow_le_pow_of_le_left (by positivity) (le_of_lt (h_yle w)) (mult w)
        _ ‚â§ (B : ‚Ñù) := by
          simp_rw [‚Üê NNReal.coe_pow, ‚Üê NNReal.coe_prod]
          exact le_of_eq (congr_arg toReal h_gprod)
    ¬∑ refine div_lt_self ?_ (by norm_num)
      simp only [pos_iff, ne_eq, ZeroMemClass.coe_eq_zero, hx]
  intro _ _
  rw [ne_eq, Nonneg.mk_eq_zero, div_eq_zero_iff, map_eq_zero, not_or, ZeroMemClass.coe_eq_zero]
  exact ‚ü®hx, by norm_num‚ü©

/-- An infinite sequence of nonzero algebraic integers of `K` satisfying the following properties:
‚Ä¢ `seq n` is nonzero;
‚Ä¢ for `w : InfinitePlace K`, `w ‚â† w‚ÇÅ ‚Üí w (seq n+1) < w (seq n)`;
‚Ä¢ `‚à£norm (seq n)‚à£ ‚â§ B`. -/
def seq : ‚Ñï ‚Üí { x : ùìû K // x ‚â† 0 }
  | 0 => ‚ü®1, by norm_num‚ü©
  | n + 1 =>
    ‚ü®(seq_next K w‚ÇÅ hB (seq n).prop).choose, (seq_next K w‚ÇÅ hB (seq n).prop).choose_spec.1‚ü©

/-- The terms of the sequence are nonzero. -/
theorem seq_ne_zero (n : ‚Ñï) : (seq K w‚ÇÅ hB n : K) ‚â† 0 := by
  refine (map_ne_zero_iff (algebraMap (ùìû K) K) ?_).mpr (seq K w‚ÇÅ hB n).prop
  exact IsFractionRing.injective { x // x ‚àà ùìû K } K

/-- The terms of the sequence have nonzero norm. -/
theorem seq_norm_ne_zero (n : ‚Ñï) : Algebra.norm ‚Ñ§ (seq K w‚ÇÅ hB n : ùìû K) ‚â† 0 :=
  Algebra.norm_ne_zero_iff.mpr (Subtype.ne_of_val_ne (seq_ne_zero K w‚ÇÅ hB n))

/-- The sequence is strictly decreasing at infinite places distinct from `w‚ÇÅ`. -/
theorem seq_decreasing {n m : ‚Ñï} (h : n < m) (w : InfinitePlace K) (hw : w ‚â† w‚ÇÅ) :
    w (seq K w‚ÇÅ hB m) < w (seq K w‚ÇÅ hB n) := by
  induction m with
  | zero =>
      exfalso
      exact Nat.not_succ_le_zero n h
  | succ m m_ih =>
      cases eq_or_lt_of_le (Nat.le_of_lt_succ h) with
      | inl hr =>
          rw [hr]
          exact (seq_next K w‚ÇÅ hB (seq K w‚ÇÅ hB m).prop).choose_spec.2.1 w hw
      | inr hr =>
          refine lt_trans ?_ (m_ih hr)
          exact (seq_next K w‚ÇÅ hB (seq K w‚ÇÅ hB m).prop).choose_spec.2.1 w hw

/-- The terms of the sequence have norm bounded by `B`. -/
theorem seq_norm_le (n : ‚Ñï) :
    Int.natAbs (Algebra.norm ‚Ñ§ (seq K w‚ÇÅ hB n : ùìû K)) ‚â§ B := by
  cases n with
  | zero =>
      have : 1 ‚â§ B := by
        contrapose! hB
        simp only [Nat.lt_one_iff.mp hB, CharP.cast_eq_zero, mul_zero, zero_le]
      simp only [ne_eq, seq, map_one, Int.natAbs_one, this]
  | succ n =>
      rw [‚Üê Nat.cast_le (Œ± := ‚Ñö), Int.cast_natAbs, Int.cast_abs, Algebra.coe_norm_int]
      exact (seq_next K w‚ÇÅ hB (seq K w‚ÇÅ hB n).prop).choose_spec.2.2

/-- Construct a unit associated to the place `w‚ÇÅ`. The family, for `w‚ÇÅ ‚â† w‚ÇÄ`, formed by the
image by the `logEmbedding` of these units  is `‚Ñù`-linearly independent, see
`unit_lattice_span_eq_top`. -/
theorem exists_unit (w‚ÇÅ : InfinitePlace K) :
    ‚àÉ u : (ùìû K)À£, ‚àÄ w : InfinitePlace K, w ‚â† w‚ÇÅ ‚Üí Real.log (w u) < 0 := by
  obtain ‚ü®B, hB‚ü© : ‚àÉ B : ‚Ñï, minkowskiBound K < (convexBodyLtFactor K) * B := by
    simp_rw [mul_comm]
    refine ENNReal.exists_nat_mul_gt ?_ ?_
    exact ne_of_gt (convexBodyLtFactor_pos K)
    exact ne_of_lt (minkowskiBound_lt_top K)
  rsuffices ‚ü®n, m, hnm, h‚ü© : ‚àÉ n m, n < m ‚àß
      (Ideal.span ({ (seq K w‚ÇÅ hB n : ùìû K) }) = Ideal.span ({ (seq K w‚ÇÅ hB m : ùìû K) }))
  ¬∑ have hu := Ideal.span_singleton_eq_span_singleton.mp h
    refine ‚ü®hu.choose, fun w hw => Real.log_neg ?_ ?_‚ü©
    ¬∑ simp only [pos_iff, ne_eq, ZeroMemClass.coe_eq_zero, ne_zero]
    ¬∑ calc
        _ = w ((seq K w‚ÇÅ hB m : K) * (seq K w‚ÇÅ hB n : K)‚Åª¬π) := by
          rw [‚Üê congr_arg ((‚Üë) : (ùìû K) ‚Üí K) hu.choose_spec, mul_comm, Submonoid.coe_mul,
            ‚Üê mul_assoc, inv_mul_cancel (seq_ne_zero K w‚ÇÅ hB n), one_mul]
        _ = w (seq K w‚ÇÅ hB m) * w (seq K w‚ÇÅ hB n)‚Åª¬π := _root_.map_mul _ _ _
        _ < 1 := by
          rw [map_inv‚ÇÄ, mul_inv_lt_iff (pos_iff.mpr (seq_ne_zero K w‚ÇÅ hB n)), mul_one]
          exact seq_decreasing K w‚ÇÅ hB hnm w hw
  refine Set.Finite.exists_lt_map_eq_of_forall_mem
    (t := { I : Ideal (ùìû K) | 1 ‚â§ Ideal.absNorm I ‚àß Ideal.absNorm I ‚â§ B })
    (fun n => ?_) ?_
  ¬∑ rw [Set.mem_setOf_eq, Ideal.absNorm_span_singleton]
    refine ‚ü®?_, seq_norm_le K w‚ÇÅ hB n‚ü©
    exact  Nat.one_le_iff_ne_zero.mpr (Int.natAbs_ne_zero.mpr (seq_norm_ne_zero K w‚ÇÅ hB n))
  ¬∑ rw [show { I : Ideal (ùìû K) | 1 ‚â§ Ideal.absNorm I ‚àß Ideal.absNorm I ‚â§ B } =
          (‚ãÉ n ‚àà Set.Icc 1 B, { I : Ideal (ùìû K) | Ideal.absNorm I = n }) by ext; simp]
    exact Set.Finite.biUnion (Set.finite_Icc _ _) (fun n hn => Ideal.finite_setOf_absNorm_eq hn.1)

theorem unitLattice_span_eq_top :
    Submodule.span ‚Ñù (unitLattice K : Set ({w : InfinitePlace K // w ‚â† w‚ÇÄ} ‚Üí ‚Ñù)) = ‚ä§ := by
  refine le_antisymm (le_top) ?_
  -- The standard basis
  let B := Pi.basisFun ‚Ñù {w : InfinitePlace K // w ‚â† w‚ÇÄ}
  -- The image by log_embedding of the family of units constructed above
  let v := fun w : { w : InfinitePlace K // w ‚â† w‚ÇÄ } => logEmbedding K (exists_unit K w).choose
  -- To prove the result, it is enough to prove that the family `v` is linearly independent
  suffices B.det v ‚â† 0 by
    rw [‚Üê isUnit_iff_ne_zero, ‚Üê is_basis_iff_det] at this
    rw [‚Üê this.2]
    exact Submodule.span_monotone (fun _ ‚ü®w, hw‚ü© =>
      ‚ü®(exists_unit K w).choose, trivial, by rw [‚Üê hw]‚ü©)
  rw [Basis.det_apply]
  -- We use a specific lemma to prove that this determinant is nonzero
  refine det_ne_zero_of_sum_col_lt_diag (fun w => ?_)
  simp_rw [Real.norm_eq_abs, Basis.coePiBasisFun.toMatrix_eq_transpose, Matrix.transpose_apply]
  rw [‚Üê sub_pos, sum_congr rfl (fun x hx => abs_of_neg ?_), sum_neg_distrib, sub_neg_eq_add,
    sum_erase_eq_sub (mem_univ _), ‚Üê add_comm_sub]
  refine add_pos_of_nonneg_of_pos ?_ ?_
  ¬∑ rw [sub_nonneg]
    exact le_abs_self _
  ¬∑ rw [sum_logEmbedding_component (exists_unit K w).choose]
    refine mul_pos_of_neg_of_neg ?_ ((exists_unit K w).choose_spec _ w.prop.symm)
    rw [mult]; split_ifs <;> norm_num
  ¬∑ refine mul_neg_of_pos_of_neg ?_ ((exists_unit K w).choose_spec x ?_)
    ¬∑ rw [mult]; split_ifs <;> norm_num
    ¬∑ exact Subtype.ext_iff_val.not.mp (ne_of_mem_erase hx)

end span_top

end dirichletUnitTheorem

section statements

variable [NumberField K]

open dirichletUnitTheorem FiniteDimensional Classical

/-- The unit rank of the number field `K`, it is equal to `card (InfinitePlace K) - 1`. -/
def rank : ‚Ñï := Fintype.card (InfinitePlace K) - 1

instance instDiscrete_unitLattice : DiscreteTopology (unitLattice K) := by
  refine discreteTopology_of_open_singleton_zero ?_
  refine isOpen_singleton_of_finite_mem_nhds 0 (s := Metric.closedBall 0 1) ?_ ?_
  ¬∑ exact Metric.closedBall_mem_nhds _ (by norm_num)
  ¬∑ refine Set.Finite.of_finite_image ?_ (Set.injOn_of_injective Subtype.val_injective _)
    convert unitLattice_inter_ball_finite K 1
    ext x
    refine ‚ü®?_, fun ‚ü®hx1, hx2‚ü© => ‚ü®‚ü®x, hx1‚ü©, hx2, rfl‚ü©‚ü©
    rintro ‚ü®x, hx, rfl‚ü©
    exact ‚ü®Subtype.mem x, hx‚ü©

protected theorem finrank_eq_rank :
    finrank ‚Ñù ({w : InfinitePlace K // w ‚â† w‚ÇÄ} ‚Üí ‚Ñù) = Units.rank K := by
  simp only [finrank_fintype_fun_eq_card, Fintype.card_subtype_compl,
    Fintype.card_ofSubsingleton, rank]

instance instModuleFree_unitLattice : Module.Free ‚Ñ§ (unitLattice K) :=
  Zlattice.module_free ‚Ñù (unitLattice_span_eq_top K)

instance instModuleFinite_unitLattice : Module.Finite ‚Ñ§ (unitLattice K) :=
  Zlattice.module_finite ‚Ñù (unitLattice_span_eq_top K)

@[simp]
theorem unitLattice_rank :
    finrank ‚Ñ§ (unitLattice K) = Units.rank K := by
  rw [‚Üê Units.finrank_eq_rank]
  exact Zlattice.rank ‚Ñù (unitLattice_span_eq_top K)

/-- The linear equivalence between `unitLattice` and `(ùìû K)À£ ‚ß∏ (torsion K)` as an additive
`‚Ñ§`-module. -/
def unitLatticeEquiv : (unitLattice K) ‚âÉ‚Çó[‚Ñ§] Additive ((ùìû K)À£ ‚ß∏ (torsion K)) := by
  refine AddEquiv.toIntLinearEquiv ?_
  rw [unitLattice, ‚Üê AddMonoidHom.range_eq_map (logEmbedding K)]
  refine (QuotientAddGroup.quotientKerEquivRange (logEmbedding K)).symm.trans ?_
  refine (QuotientAddGroup.quotientAddEquivOfEq ?_).trans
    (QuotientAddGroup.quotientKerEquivOfSurjective
      (MonoidHom.toAdditive (QuotientGroup.mk' (torsion K))) (fun x => ?_))
  ¬∑ ext
    rw [AddMonoidHom.mem_ker, AddMonoidHom.mem_ker, logEmbedding_eq_zero_iff,
      MonoidHom.toAdditive_apply_apply, ofMul_eq_zero, QuotientGroup.mk'_apply,
      QuotientGroup.eq_one_iff]
    rfl
  ¬∑ refine ‚ü®Additive.ofMul x.out', ?_‚ü©
    simp only [MonoidHom.toAdditive_apply_apply, toMul_ofMul, QuotientGroup.mk'_apply,
      QuotientGroup.out_eq']
    rfl

instance : Module.Free ‚Ñ§ (Additive ((ùìû K)À£ ‚ß∏ (torsion K))) :=
  (instModuleFree_unitLattice K).of_equiv' (unitLatticeEquiv K)

instance : Module.Finite ‚Ñ§ (Additive ((ùìû K)À£ ‚ß∏ (torsion K))) :=
  Module.Finite.equiv (unitLatticeEquiv K)

theorem rank_modTorsion :
    FiniteDimensional.finrank ‚Ñ§ (Additive ((ùìû K)À£ ‚ß∏ (torsion K))) = rank K := by
  rw [‚Üê LinearEquiv.finrank_eq (unitLatticeEquiv K), unitLattice_rank]

/-- A basis of the quotient `(ùìû K)À£ ‚ß∏ (torsion K)` seen as an additive ‚Ñ§-module. -/
def basisModTorsion : Basis (Fin (rank K)) ‚Ñ§ (Additive ((ùìû K)À£ ‚ß∏ (torsion K))) :=
  Basis.reindex (Module.Free.chooseBasis ‚Ñ§ _) (Fintype.equivOfCardEq <| by
    rw [‚Üê FiniteDimensional.finrank_eq_card_chooseBasisIndex, rank_modTorsion, Fintype.card_fin])

/-- A fundamental system of units of `K`. The units of `fundSystem` are arbitrary lifts of the
units in `basisModTorsion`. -/
def fundSystem : Fin (rank K) ‚Üí (ùìû K)À£ :=
  fun i => Quotient.out' (Additive.toMul (basisModTorsion K i))

/-- The exponents that appear in the unique decomposition of a unit as the product of
a root of unity and powers of the units of the fundamental system `fundSystem` (see
`exist_unique_eq_mul_prod`) are given by the representation of the unit of `basisModTorsion`. -/
theorem fun_eq_repr {x Œ∂ : (ùìû K)À£} {f : Fin (rank K) ‚Üí ‚Ñ§} (hŒ∂ : Œ∂ ‚àà torsion K)
    (h : x = Œ∂ * ‚àè i, (fundSystem K i) ^ (f i)) :
    f = (basisModTorsion K).repr (Additive.ofMul ‚Üëx) := by
  suffices Additive.ofMul ‚Üëx = ‚àë i, (f i) ‚Ä¢ (basisModTorsion K i) by
    rw [‚Üê (basisModTorsion K).repr_sum_self f, ‚Üê this]
  calc
    Additive.ofMul ‚Üëx = ‚àë i, (f i) ‚Ä¢ Additive.ofMul ‚Üë(fundSystem K i) := by
                      rw [h, QuotientGroup.mk_mul, (QuotientGroup.eq_one_iff _).mpr hŒ∂, one_mul,
                        QuotientGroup.mk_prod, ofMul_prod]; rfl
                    _ = ‚àë i, (f i) ‚Ä¢ (basisModTorsion K i) := by
                      simp_rw [fundSystem, QuotientGroup.out_eq', ofMul_toMul]

/-- **Dirichlet Unit Theorem**. Any unit `x` of `ùìû K` can be written uniquely as the product of
a root of unity and powers of the units of the fundamental system `fundSystem`. -/
theorem exist_unique_eq_mul_prod (x : (ùìû K)À£) : ‚àÉ! (Œ∂ : torsion K) (e : Fin (rank K) ‚Üí ‚Ñ§),
    x = Œ∂ * ‚àè i, (fundSystem K i) ^ (e i) := by
  let Œ∂ := x * (‚àè i, (fundSystem K i) ^ ((basisModTorsion K).repr (Additive.ofMul ‚Üëx) i))‚Åª¬π
  have h_tors : Œ∂ ‚àà torsion K := by
    rw [‚Üê QuotientGroup.eq_one_iff, QuotientGroup.mk_mul, QuotientGroup.mk_inv, ‚Üê ofMul_eq_zero,
      ofMul_mul, ofMul_inv, QuotientGroup.mk_prod, ofMul_prod]
    simp_rw [QuotientGroup.mk_zpow, ofMul_zpow, fundSystem, QuotientGroup.out_eq', ofMul_toMul]
    rw [add_eq_zero_iff_eq_neg, neg_neg]
    exact ((basisModTorsion K).sum_repr (Additive.ofMul ‚Üëx)).symm
  refine ‚ü®‚ü®Œ∂, h_tors‚ü©, ?_, ?_‚ü©
  ¬∑ refine ‚ü®((basisModTorsion K).repr (Additive.ofMul ‚Üëx) : Fin (rank K) ‚Üí ‚Ñ§), ?_, ?_‚ü©
    ¬∑ simp only [_root_.inv_mul_cancel_right]
    ¬∑ exact fun _ hf => fun_eq_repr K h_tors hf
  ¬∑ rintro Œ∑ ‚ü®_, hf, _‚ü©
    simp_rw [fun_eq_repr K Œ∑.prop hf] at hf
    ext1; dsimp only
    nth_rewrite 1 [hf]
    rw [_root_.mul_inv_cancel_right]

end statements

end NumberField.Units
