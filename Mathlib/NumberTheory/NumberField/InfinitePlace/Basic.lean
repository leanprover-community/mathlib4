/-
Copyright (c) 2022 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.NumberTheory.NumberField.InfinitePlace.Embeddings
import Mathlib.NumberTheory.NumberField.Norm
import Mathlib.RingTheory.RootsOfUnity.PrimitiveRoots
import Mathlib.Topology.Instances.Complex

/-!
# Infinite places of a number field

This file defines the infinite places of a number field.

## Main Definitions and Results

* `NumberField.InfinitePlace`: the type of infinite places of a number field `K`.
* `NumberField.InfinitePlace.mk_eq_iff`: two complex embeddings define the same infinite place iff
  they are equal or complex conjugates.
* `NumberField.InfinitePlace.IsReal`: The predicate on infinite places saying
  that a place is real, i.e., defined by a real embedding.
* `NumberField.InfinitePlace.IsComplex`: The predicate on infinite places saying
  that a place is complex, i.e., defined by a complex embedding that is not real.
* `NumberField.InfinitePlace.mult`: the multiplicity of an infinite place, that is the number of
  distinct complex embeddings that define it. So it is equal to `1` if the place is real and `2`
  if the place is complex.
* `NumberField.InfinitePlace.prod_eq_abs_norm`: the infinite part of the product formula, that is
  for `x ‚àà K`, we have `Œ†_w ‚Äñx‚Äñ_w = |norm(x)|` where the product is over the infinite place `w` and
  `‚Äñ¬∑‚Äñ_w` is the normalized absolute value for `w`.
* `NumberField.InfinitePlace.card_add_two_mul_card_eq_rank`: the degree of `K` is equal to the
  number of real places plus twice the number of complex places.

## Tags

number field, infinite places
-/

open scoped Finset

open NumberField Fintype Module

variable {k : Type*} [Field k] (K : Type*) [Field K] {F : Type*} [Field F]

/-- An infinite place of a number field `K` is a place associated to a complex embedding. -/
def NumberField.InfinitePlace := { w : AbsoluteValue K ‚Ñù // ‚àÉ œÜ : K ‚Üí+* ‚ÑÇ, place œÜ = w }

instance [NumberField K] : Nonempty (NumberField.InfinitePlace K) := Set.instNonemptyRange _

variable {K}

/-- Return the infinite place defined by a complex embedding `œÜ`. -/
noncomputable def NumberField.InfinitePlace.mk (œÜ : K ‚Üí+* ‚ÑÇ) : NumberField.InfinitePlace K :=
  ‚ü®place œÜ, ‚ü®œÜ, rfl‚ü©‚ü©

namespace NumberField.InfinitePlace

instance {K : Type*} [Field K] : FunLike (InfinitePlace K) K ‚Ñù where
  coe w x := w.1 x
  coe_injective' _ _ h := Subtype.eq (AbsoluteValue.ext fun x => congr_fun h x)

lemma coe_apply {K : Type*} [Field K] (v : InfinitePlace K) (x : K) : v x = v.1 x := rfl

@[ext]
lemma ext {K : Type*} [Field K] (v‚ÇÅ v‚ÇÇ : InfinitePlace K) (h : ‚àÄ k, v‚ÇÅ k = v‚ÇÇ k) : v‚ÇÅ = v‚ÇÇ :=
  Subtype.ext <| AbsoluteValue.ext h

instance : MonoidWithZeroHomClass (InfinitePlace K) K ‚Ñù where
  map_mul w _ _ := w.1.map_mul _ _
  map_one w := w.1.map_one
  map_zero w := w.1.map_zero

instance : NonnegHomClass (InfinitePlace K) K ‚Ñù where
  apply_nonneg w _ := w.1.nonneg _

@[simp]
theorem apply (œÜ : K ‚Üí+* ‚ÑÇ) (x : K) : (mk œÜ) x = ‚ÄñœÜ x‚Äñ := rfl

/-- For an infinite place `w`, return an embedding `œÜ` such that `w = infinite_place œÜ` . -/
noncomputable def embedding (w : InfinitePlace K) : K ‚Üí+* ‚ÑÇ := w.2.choose

@[simp]
theorem mk_embedding (w : InfinitePlace K) : mk (embedding w) = w := Subtype.ext w.2.choose_spec

@[simp]
theorem mk_conjugate_eq (œÜ : K ‚Üí+* ‚ÑÇ) : mk (ComplexEmbedding.conjugate œÜ) = mk œÜ := by
  refine DFunLike.ext _ _ (fun x => ?_)
  rw [apply, apply, ComplexEmbedding.conjugate_coe_eq, Complex.norm_conj]

theorem norm_embedding_eq (w : InfinitePlace K) (x : K) :
    ‚Äñ(embedding w) x‚Äñ = w x := by
  nth_rewrite 2 [‚Üê mk_embedding w]
  rfl

variable (K) in
theorem embedding_injective : (embedding (K := K)).Injective :=
  fun _ _ h ‚Ü¶ by simpa using congr_arg mk h

@[simp]
theorem embedding_inj {v‚ÇÅ v‚ÇÇ : InfinitePlace K} : v‚ÇÅ.embedding = v‚ÇÇ.embedding ‚Üî v‚ÇÅ = v‚ÇÇ :=
  (embedding_injective _).eq_iff

variable (K) in
theorem conjugate_embedding_injective :
    (fun (v : InfinitePlace K) ‚Ü¶ ComplexEmbedding.conjugate v.embedding).Injective :=
  star_injective.comp <| embedding_injective K

variable (K) in
theorem eq_of_embedding_eq_conjugate {v‚ÇÅ v‚ÇÇ : InfinitePlace K}
    (h : v‚ÇÅ.embedding = ComplexEmbedding.conjugate v‚ÇÇ.embedding) : v‚ÇÅ = v‚ÇÇ := by
  rw [‚Üê mk_embedding v‚ÇÅ, h, mk_conjugate_eq, mk_embedding]

theorem eq_iff_eq (x : K) (r : ‚Ñù) : (‚àÄ w : InfinitePlace K, w x = r) ‚Üî ‚àÄ œÜ : K ‚Üí+* ‚ÑÇ, ‚ÄñœÜ x‚Äñ = r :=
  ‚ü®fun hw œÜ => hw (mk œÜ), by rintro hœÜ ‚ü®w, ‚ü®œÜ, rfl‚ü©‚ü©; exact hœÜ œÜ‚ü©

theorem le_iff_le (x : K) (r : ‚Ñù) : (‚àÄ w : InfinitePlace K, w x ‚â§ r) ‚Üî ‚àÄ œÜ : K ‚Üí+* ‚ÑÇ, ‚ÄñœÜ x‚Äñ ‚â§ r :=
  ‚ü®fun hw œÜ => hw (mk œÜ), by rintro hœÜ ‚ü®w, ‚ü®œÜ, rfl‚ü©‚ü©; exact hœÜ œÜ‚ü©

theorem pos_iff {w : InfinitePlace K} {x : K} : 0 < w x ‚Üî x ‚â† 0 := AbsoluteValue.pos_iff w.1

@[simp]
theorem mk_eq_iff {œÜ œà : K ‚Üí+* ‚ÑÇ} : mk œÜ = mk œà ‚Üî œÜ = œà ‚à® ComplexEmbedding.conjugate œÜ = œà := by
  constructor
  ¬∑ -- We prove that the map œà ‚àò œÜ‚Åª¬π between œÜ(K) and ‚ÑÇ is uniform continuous, thus it is either the
    -- inclusion or the complex conjugation using `Complex.uniformContinuous_ringHom_eq_id_or_conj`
    intro h‚ÇÄ
    obtain ‚ü®j, hiœÜ‚ü© := (œÜ.injective).hasLeftInverse
    let Œπ := RingEquiv.ofLeftInverse hiœÜ
    have hlip : LipschitzWith 1 (RingHom.comp œà Œπ.symm.toRingHom) := by
      change LipschitzWith 1 (œà ‚àò Œπ.symm)
      apply LipschitzWith.of_dist_le_mul
      intro x y
      rw [NNReal.coe_one, one_mul, NormedField.dist_eq, Function.comp_apply, Function.comp_apply,
        ‚Üê map_sub, ‚Üê map_sub]
      apply le_of_eq
      suffices ‚ÄñœÜ (Œπ.symm (x - y))‚Äñ = ‚Äñœà (Œπ.symm (x - y))‚Äñ by
        rw [‚Üê this, ‚Üê RingEquiv.ofLeftInverse_apply hiœÜ _, RingEquiv.apply_symm_apply Œπ _]
        rfl
      exact congrFun (congrArg (‚Üë) h‚ÇÄ) _
    cases
      Complex.uniformContinuous_ringHom_eq_id_or_conj œÜ.fieldRange hlip.uniformContinuous with
    | inl h =>
        left; ext1 x
        conv_rhs => rw [‚Üê hiœÜ x]
        exact (congrFun h (Œπ x)).symm
    | inr h =>
        right; ext1 x
        conv_rhs => rw [‚Üê hiœÜ x]
        exact (congrFun h (Œπ x)).symm
  ¬∑ rintro (‚ü®h‚ü© | ‚ü®h‚ü©)
    ¬∑ exact congr_arg mk h
    ¬∑ rw [‚Üê mk_conjugate_eq]
      exact congr_arg mk h

/-- An infinite place is real if it is defined by a real embedding. -/
def IsReal (w : InfinitePlace K) : Prop := ‚àÉ œÜ : K ‚Üí+* ‚ÑÇ, ComplexEmbedding.IsReal œÜ ‚àß mk œÜ = w

/-- An infinite place is complex if it is defined by a complex (i.e. not real) embedding. -/
def IsComplex (w : InfinitePlace K) : Prop := ‚àÉ œÜ : K ‚Üí+* ‚ÑÇ, ¬¨ComplexEmbedding.IsReal œÜ ‚àß mk œÜ = w

theorem embedding_mk_eq (œÜ : K ‚Üí+* ‚ÑÇ) :
    embedding (mk œÜ) = œÜ ‚à® embedding (mk œÜ) = ComplexEmbedding.conjugate œÜ := by
  rw [@eq_comm _ _ œÜ, @eq_comm _ _ (ComplexEmbedding.conjugate œÜ), ‚Üê mk_eq_iff, mk_embedding]

@[simp]
theorem embedding_mk_eq_of_isReal {œÜ : K ‚Üí+* ‚ÑÇ} (h : ComplexEmbedding.IsReal œÜ) :
    embedding (mk œÜ) = œÜ := by
  have := embedding_mk_eq œÜ
  rwa [ComplexEmbedding.isReal_iff.mp h, or_self] at this

theorem isReal_iff {w : InfinitePlace K} :
    IsReal w ‚Üî ComplexEmbedding.IsReal (embedding w) := by
  refine ‚ü®?_, fun h => ‚ü®embedding w, h, mk_embedding w‚ü©‚ü©
  rintro ‚ü®œÜ, ‚ü®hœÜ, rfl‚ü©‚ü©
  rwa [embedding_mk_eq_of_isReal hœÜ]

theorem isComplex_iff {w : InfinitePlace K} :
    IsComplex w ‚Üî ¬¨ComplexEmbedding.IsReal (embedding w) := by
  refine ‚ü®?_, fun h => ‚ü®embedding w, h, mk_embedding w‚ü©‚ü©
  rintro ‚ü®œÜ, ‚ü®hœÜ, rfl‚ü©‚ü©
  contrapose! hœÜ
  cases mk_eq_iff.mp (mk_embedding (mk œÜ)) with
  | inl h => rwa [h] at hœÜ
  | inr h => rwa [‚Üê ComplexEmbedding.isReal_conjugate_iff, h] at hœÜ

@[simp]
theorem conjugate_embedding_eq_of_isReal {w : InfinitePlace K} (h : IsReal w) :
    ComplexEmbedding.conjugate (embedding w) = embedding w :=
  ComplexEmbedding.isReal_iff.mpr (isReal_iff.mp h)

@[simp]
theorem not_isReal_iff_isComplex {w : InfinitePlace K} : ¬¨IsReal w ‚Üî IsComplex w := by
  rw [isComplex_iff, isReal_iff]

@[simp]
theorem not_isComplex_iff_isReal {w : InfinitePlace K} : ¬¨IsComplex w ‚Üî IsReal w := by
  rw [isComplex_iff, isReal_iff, not_not]

theorem isReal_or_isComplex (w : InfinitePlace K) : IsReal w ‚à® IsComplex w := by
  rw [‚Üê not_isReal_iff_isComplex]; exact em _

theorem ne_of_isReal_isComplex {w w' : InfinitePlace K} (h : IsReal w) (h' : IsComplex w') :
    w ‚â† w' := fun h_eq ‚Ü¶ not_isReal_iff_isComplex.mpr h' (h_eq ‚ñ∏ h)

variable (K) in
theorem disjoint_isReal_isComplex :
    Disjoint {(w : InfinitePlace K) | IsReal w} {(w : InfinitePlace K) | IsComplex w} :=
  Set.disjoint_iff.2 <| fun _ hw ‚Ü¶ not_isReal_iff_isComplex.2 hw.2 hw.1

/-- The real embedding associated to a real infinite place. -/
noncomputable def embedding_of_isReal {w : InfinitePlace K} (hw : IsReal w) : K ‚Üí+* ‚Ñù :=
  ComplexEmbedding.IsReal.embedding (isReal_iff.mp hw)

@[simp]
theorem embedding_of_isReal_apply {w : InfinitePlace K} (hw : IsReal w) (x : K) :
    ((embedding_of_isReal hw) x : ‚ÑÇ) = (embedding w) x :=
  ComplexEmbedding.IsReal.coe_embedding_apply (isReal_iff.mp hw) x

theorem norm_embedding_of_isReal {w : InfinitePlace K} (hw : IsReal w) (x : K) :
    ‚Äñembedding_of_isReal hw x‚Äñ = w x := by
  rw [‚Üê norm_embedding_eq, ‚Üê embedding_of_isReal_apply hw, Complex.norm_real]

@[simp]
theorem isReal_of_mk_isReal {œÜ : K ‚Üí+* ‚ÑÇ} (h : IsReal (mk œÜ)) :
    ComplexEmbedding.IsReal œÜ := by
  contrapose! h
  rw [not_isReal_iff_isComplex]
  exact ‚ü®œÜ, h, rfl‚ü©

lemma isReal_mk_iff {œÜ : K ‚Üí+* ‚ÑÇ} :
    IsReal (mk œÜ) ‚Üî ComplexEmbedding.IsReal œÜ :=
  ‚ü®isReal_of_mk_isReal, fun H ‚Ü¶ ‚ü®_, H, rfl‚ü©‚ü©

lemma isComplex_mk_iff {œÜ : K ‚Üí+* ‚ÑÇ} :
    IsComplex (mk œÜ) ‚Üî ¬¨ ComplexEmbedding.IsReal œÜ :=
  not_isReal_iff_isComplex.symm.trans isReal_mk_iff.not

@[simp]
theorem not_isReal_of_mk_isComplex {œÜ : K ‚Üí+* ‚ÑÇ} (h : IsComplex (mk œÜ)) :
    ¬¨ ComplexEmbedding.IsReal œÜ := by rwa [‚Üê isComplex_mk_iff]

open scoped Classical in
/-- The multiplicity of an infinite place, that is the number of distinct complex embeddings that
define it, see `card_filter_mk_eq`. -/
noncomputable def mult (w : InfinitePlace K) : ‚Ñï := if (IsReal w) then 1 else 2

@[simp]
theorem mult_isReal (w : {w : InfinitePlace K // IsReal w}) :
    mult w.1 = 1 := by
  rw [mult, if_pos w.prop]

@[simp]
theorem mult_isComplex (w : {w : InfinitePlace K // IsComplex w}) :
    mult w.1 = 2 := by
  rw [mult, if_neg (not_isReal_iff_isComplex.mpr w.prop)]

theorem mult_pos {w : InfinitePlace K} : 0 < mult w := by
  rw [mult]
  split_ifs <;> norm_num

@[simp]
theorem mult_ne_zero {w : InfinitePlace K} : mult w ‚â† 0 := ne_of_gt mult_pos

theorem mult_coe_ne_zero {w : InfinitePlace K} : (mult w : ‚Ñù) ‚â† 0 :=
  Nat.cast_ne_zero.mpr mult_ne_zero

theorem one_le_mult {w : InfinitePlace K} : (1 : ‚Ñù) ‚â§ mult w := by
  rw [‚Üê Nat.cast_one, Nat.cast_le]
  exact mult_pos

open scoped Classical in
theorem card_filter_mk_eq [NumberField K] (w : InfinitePlace K) : #{œÜ | mk œÜ = w} = mult w := by
  conv_lhs =>
    congr; congr; ext
    rw [‚Üê mk_embedding w, mk_eq_iff, ComplexEmbedding.conjugate, star_involutive.eq_iff]
  simp_rw [Finset.filter_or, Finset.filter_eq' _ (embedding w),
    Finset.filter_eq' _ (ComplexEmbedding.conjugate (embedding w)),
    Finset.mem_univ, ite_true, mult]
  split_ifs with hw
  ¬∑ rw [ComplexEmbedding.isReal_iff.mp (isReal_iff.mp hw), Finset.union_idempotent,
      Finset.card_singleton]
  ¬∑ refine Finset.card_pair ?_
    rwa [Ne, eq_comm, ‚Üê ComplexEmbedding.isReal_iff, ‚Üê isReal_iff]

open scoped Classical in
noncomputable instance NumberField.InfinitePlace.fintype [NumberField K] :
    Fintype (InfinitePlace K) := Set.fintypeRange _

open scoped Classical in
@[to_additive]
theorem prod_eq_prod_mul_prod {Œ± : Type*} [CommMonoid Œ±] [NumberField K] (f : InfinitePlace K ‚Üí Œ±) :
    ‚àè w, f w = (‚àè w : {w // IsReal w}, f w.1) * (‚àè w : {w // IsComplex w}, f w.1) := by
  rw [‚Üê Equiv.prod_comp (Equiv.subtypeEquivRight (fun _ ‚Ü¶ not_isReal_iff_isComplex))]
  simp [Fintype.prod_subtype_mul_prod_subtype]

theorem sum_mult_eq [NumberField K] :
    ‚àë w : InfinitePlace K, mult w = Module.finrank ‚Ñö K := by
  classical
  rw [‚Üê Embeddings.card K ‚ÑÇ, Fintype.card, Finset.card_eq_sum_ones, ‚Üê Finset.univ.sum_fiberwise
    (fun œÜ => InfinitePlace.mk œÜ)]
  exact Finset.sum_congr rfl
    (fun _ _ => by rw [Finset.sum_const, smul_eq_mul, mul_one, card_filter_mk_eq])

/-- The map from real embeddings to real infinite places as an equiv -/
noncomputable def mkReal :
    { œÜ : K ‚Üí+* ‚ÑÇ // ComplexEmbedding.IsReal œÜ } ‚âÉ { w : InfinitePlace K // IsReal w } := by
  refine (Equiv.ofBijective (fun œÜ => ‚ü®mk œÜ, ?_‚ü©) ‚ü®fun œÜ œà h => ?_, fun w => ?_‚ü©)
  ¬∑ exact ‚ü®œÜ, œÜ.prop, rfl‚ü©
  ¬∑ rwa [Subtype.mk.injEq, mk_eq_iff, ComplexEmbedding.isReal_iff.mp œÜ.prop, or_self,
      ‚Üê Subtype.ext_iff] at h
  ¬∑ exact ‚ü®‚ü®embedding w, isReal_iff.mp w.prop‚ü©, by simp‚ü©

/-- The map from nonreal embeddings to complex infinite places -/
noncomputable def mkComplex :
    { œÜ : K ‚Üí+* ‚ÑÇ // ¬¨ComplexEmbedding.IsReal œÜ } ‚Üí { w : InfinitePlace K // IsComplex w } :=
  Subtype.map mk fun œÜ hœÜ => ‚ü®œÜ, hœÜ, rfl‚ü©

@[simp]
theorem mkReal_coe (œÜ : { œÜ : K ‚Üí+* ‚ÑÇ // ComplexEmbedding.IsReal œÜ }) :
    (mkReal œÜ : InfinitePlace K) = mk (œÜ : K ‚Üí+* ‚ÑÇ) := rfl

@[simp]
theorem mkComplex_coe (œÜ : { œÜ : K ‚Üí+* ‚ÑÇ // ¬¨ComplexEmbedding.IsReal œÜ }) :
    (mkComplex œÜ : InfinitePlace K) = mk (œÜ : K ‚Üí+* ‚ÑÇ) := rfl

variable [NumberField K]

/-- The infinite part of the product formula : for `x ‚àà K`, we have `Œ†_w ‚Äñx‚Äñ_w = |norm(x)|` where
`‚Äñ¬∑‚Äñ_w` is the normalized absolute value for `w`. -/
theorem prod_eq_abs_norm (x : K) :
    ‚àè w : InfinitePlace K, w x ^ mult w = abs (Algebra.norm ‚Ñö x) := by
  classical
  convert (congr_arg (‚Äñ¬∑‚Äñ) (Algebra.norm_eq_prod_embeddings ‚Ñö ‚ÑÇ x)).symm
  ¬∑ rw [norm_prod, ‚Üê Fintype.prod_equiv RingHom.equivRatAlgHom (fun f => ‚Äñf x‚Äñ)
      (fun œÜ => ‚ÄñœÜ x‚Äñ) fun _ => by simp [RingHom.equivRatAlgHom_apply]]
    rw [‚Üê Finset.prod_fiberwise Finset.univ mk (fun œÜ => ‚ÄñœÜ x‚Äñ)]
    have (w : InfinitePlace K) (œÜ) (hœÜ : œÜ ‚àà ({œÜ | mk œÜ = w} : Finset _)) :
        ‚ÄñœÜ x‚Äñ = w x := by rw [‚Üê (Finset.mem_filter.mp hœÜ).2, apply]
    simp_rw [Finset.prod_congr rfl (this _), Finset.prod_const, card_filter_mk_eq]
  ¬∑ rw [eq_ratCast, Rat.cast_abs, ‚Üê Real.norm_eq_abs, ‚Üê Complex.norm_real, Complex.ofReal_ratCast]

theorem one_le_of_lt_one {w : InfinitePlace K} {a : (ùìû K)} (ha : a ‚â† 0)
    (h : ‚àÄ ‚¶Éz‚¶Ñ, z ‚â† w ‚Üí z a < 1) : 1 ‚â§ w a := by
  suffices (1 : ‚Ñù) ‚â§ |Algebra.norm ‚Ñö (a : K)| by
    contrapose! this
    rw [‚Üê InfinitePlace.prod_eq_abs_norm, ‚Üê Finset.prod_const_one]
    refine Finset.prod_lt_prod_of_nonempty (fun _ _ ‚Ü¶ ?_) (fun z _ ‚Ü¶ ?_) Finset.univ_nonempty
    ¬∑ exact pow_pos (pos_iff.mpr ((Subalgebra.coe_eq_zero _).not.mpr ha)) _
    ¬∑ refine pow_lt_one‚ÇÄ (apply_nonneg _ _) ?_ (by rw [mult]; split_ifs <;> norm_num)
      by_cases hz : z = w
      ¬∑ rwa [hz]
      ¬∑ exact h hz
  rw [‚Üê Algebra.coe_norm_int, ‚Üê Int.cast_one, ‚Üê Int.cast_abs, Rat.cast_intCast, Int.cast_le]
  exact Int.one_le_abs (Algebra.norm_ne_zero_iff.mpr ha)

open scoped IntermediateField in
theorem _root_.NumberField.is_primitive_element_of_infinitePlace_lt {x : ùìû K}
    {w : InfinitePlace K} (h‚ÇÅ : x ‚â† 0) (h‚ÇÇ : ‚àÄ ‚¶Éw'‚¶Ñ, w' ‚â† w ‚Üí w' x < 1)
    (h‚ÇÉ : IsReal w ‚à® |(w.embedding x).re| < 1) : ‚Ñö‚üÆ(x : K)‚üØ = ‚ä§ := by
  rw [Field.primitive_element_iff_algHom_eq_of_eval ‚Ñö ‚ÑÇ ?_ _ w.embedding.toRatAlgHom]
  ¬∑ intro œà hœà
    have h : 1 ‚â§ w x := one_le_of_lt_one h‚ÇÅ h‚ÇÇ
    have main : w = InfinitePlace.mk œà.toRingHom := by
      simp only [RingHom.toRatAlgHom_apply] at hœà
      rw [‚Üê norm_embedding_eq, hœà] at h
      contrapose! h
      exact h‚ÇÇ h.symm
    rw [(mk_embedding w).symm, mk_eq_iff] at main
    cases h‚ÇÉ with
    | inl hw =>
      rw [conjugate_embedding_eq_of_isReal hw, or_self] at main
      exact congr_arg RingHom.toRatAlgHom main
    | inr hw =>
      refine congr_arg RingHom.toRatAlgHom (main.resolve_right fun h' ‚Ü¶ hw.not_ge ?_)
      have : (embedding w x).im = 0 := by
        rw [‚Üê Complex.conj_eq_iff_im]
        have := RingHom.congr_fun h' x
        simp at this
        rw [this]
        exact hœà.symm
      rwa [‚Üê norm_embedding_eq, ‚Üê Complex.re_add_im (embedding w x), this, Complex.ofReal_zero,
        zero_mul, add_zero, Complex.norm_real] at h
  ¬∑ exact fun x ‚Ü¶ IsAlgClosed.splits_codomain (minpoly ‚Ñö x)

theorem _root_.NumberField.adjoin_eq_top_of_infinitePlace_lt {x : ùìû K} {w : InfinitePlace K}
    (h‚ÇÅ : x ‚â† 0) (h‚ÇÇ : ‚àÄ ‚¶Éw'‚¶Ñ, w' ‚â† w ‚Üí w' x < 1) (h‚ÇÉ : IsReal w ‚à® |(w.embedding x).re| < 1) :
    Algebra.adjoin ‚Ñö {(x : K)} = ‚ä§ := by
  rw [‚Üê IntermediateField.adjoin_simple_toSubalgebra_of_integral (IsIntegral.of_finite ‚Ñö _)]
  exact congr_arg IntermediateField.toSubalgebra <|
    NumberField.is_primitive_element_of_infinitePlace_lt h‚ÇÅ h‚ÇÇ h‚ÇÉ

variable (K)

open scoped Classical in
/-- The number of infinite real places of the number field `K`. -/
noncomputable abbrev nrRealPlaces := card { w : InfinitePlace K // IsReal w }

open scoped Classical in
/-- The number of infinite complex places of the number field `K`. -/
noncomputable abbrev nrComplexPlaces := card { w : InfinitePlace K // IsComplex w }

open scoped Classical in
theorem card_real_embeddings :
    card { œÜ : K ‚Üí+* ‚ÑÇ // ComplexEmbedding.IsReal œÜ } = nrRealPlaces K := Fintype.card_congr mkReal

theorem card_eq_nrRealPlaces_add_nrComplexPlaces :
    Fintype.card (InfinitePlace K) = nrRealPlaces K + nrComplexPlaces K := by
  classical
  convert Fintype.card_subtype_or_disjoint (IsReal (K := K)) (IsComplex (K := K))
    (disjoint_isReal_isComplex K) using 1
  exact (Fintype.card_of_subtype _ (fun w ‚Ü¶ ‚ü®fun _ ‚Ü¶ isReal_or_isComplex w, fun _ ‚Ü¶ by simp‚ü©)).symm

open scoped Classical in
theorem card_complex_embeddings :
    card { œÜ : K ‚Üí+* ‚ÑÇ // ¬¨ComplexEmbedding.IsReal œÜ } = 2 * nrComplexPlaces K := by
  suffices ‚àÄ w : { w : InfinitePlace K // IsComplex w },
     #{œÜ : {œÜ //¬¨ ComplexEmbedding.IsReal œÜ} | mkComplex œÜ = w} = 2 by
    rw [Fintype.card, Finset.card_eq_sum_ones, ‚Üê Finset.sum_fiberwise _ (fun œÜ => mkComplex œÜ)]
    simp_rw [Finset.sum_const, this, smul_eq_mul, mul_one, Fintype.card, Finset.card_eq_sum_ones,
      Finset.mul_sum, Finset.sum_const, smul_eq_mul, mul_one]
  rintro ‚ü®w, hw‚ü©
  convert card_filter_mk_eq w
  ¬∑ rw [‚Üê Fintype.card_subtype, ‚Üê Fintype.card_subtype]
    refine Fintype.card_congr (Equiv.ofBijective ?_ ‚ü®fun _ _ h => ?_, fun ‚ü®œÜ, hœÜ‚ü© => ?_‚ü©)
    ¬∑ exact fun ‚ü®œÜ, hœÜ‚ü© => ‚ü®œÜ.val, by rwa [Subtype.ext_iff] at hœÜ‚ü©
    ¬∑ rwa [Subtype.mk_eq_mk, ‚Üê Subtype.ext_iff, ‚Üê Subtype.ext_iff] at h
    ¬∑ refine ‚ü®‚ü®‚ü®œÜ, not_isReal_of_mk_isComplex (hœÜ.symm ‚ñ∏ hw)‚ü©, ?_‚ü©, rfl‚ü©
      rwa [Subtype.ext_iff, mkComplex_coe]
  ¬∑ simp_rw [mult, not_isReal_iff_isComplex.mpr hw, ite_false]

theorem card_add_two_mul_card_eq_rank :
    nrRealPlaces K + 2 * nrComplexPlaces K = finrank ‚Ñö K := by
  classical
  rw [‚Üê card_real_embeddings, ‚Üê card_complex_embeddings, Fintype.card_subtype_compl,
    ‚Üê Embeddings.card K ‚ÑÇ, Nat.add_sub_of_le]
  exact Fintype.card_subtype_le _

variable {K}

theorem nrComplexPlaces_eq_zero_of_finrank_eq_one (h : finrank ‚Ñö K = 1) :
    nrComplexPlaces K = 0 := by linarith [card_add_two_mul_card_eq_rank K]

theorem nrRealPlaces_eq_one_of_finrank_eq_one (h : finrank ‚Ñö K = 1) :
    nrRealPlaces K = 1 := by
  have := card_add_two_mul_card_eq_rank K
  rwa [nrComplexPlaces_eq_zero_of_finrank_eq_one h, h, mul_zero, add_zero] at this

theorem nrRealPlaces_pos_of_odd_finrank (h : Odd (finrank ‚Ñö K)) :
    0 < nrRealPlaces K := by
  refine Nat.pos_of_ne_zero ?_
  by_contra hc
  refine (Nat.not_odd_iff_even.mpr ?_) h
  rw [‚Üê card_add_two_mul_card_eq_rank, hc, zero_add]
  exact even_two_mul (nrComplexPlaces K)

namespace IsPrimitiveRoot

variable {Œ∂ : K} {k : ‚Ñï}

theorem nrRealPlaces_eq_zero_of_two_lt (hk : 2 < k) (hŒ∂ : IsPrimitiveRoot Œ∂ k) :
    NumberField.InfinitePlace.nrRealPlaces K = 0 := by
  refine (@Fintype.card_eq_zero_iff _ (_)).2 ‚ü®fun ‚ü®w, hwreal‚ü© ‚Ü¶ ?_‚ü©
  rw [NumberField.InfinitePlace.isReal_iff] at hwreal
  let f := w.embedding
  have hŒ∂' : IsPrimitiveRoot (f Œ∂) k := hŒ∂.map_of_injective f.injective
  have him : (f Œ∂).im = 0 := by
    rw [‚Üê Complex.conj_eq_iff_im, ‚Üê NumberField.ComplexEmbedding.conjugate_coe_eq]
    congr
  have hre : (f Œ∂).re = 1 ‚à® (f Œ∂).re = -1 := by
    rw [‚Üê Complex.abs_re_eq_norm] at him
    have := Complex.norm_eq_one_of_pow_eq_one hŒ∂'.pow_eq_one (by cutsat)
    rwa [‚Üê him, ‚Üê abs_one, abs_eq_abs] at this
  cases hre with
  | inl hone =>
    exact hŒ∂'.ne_one (by cutsat) <| Complex.ext (by simp [hone]) (by simp [him])
  | inr hnegone =>
    replace hŒ∂' := hŒ∂'.eq_orderOf
    simp only [show f Œ∂ = -1 from Complex.ext (by simp [hnegone]) (by simp [him]),
      orderOf_neg_one, ringChar.eq_zero, OfNat.zero_ne_ofNat, ‚ÜìreduceIte] at hŒ∂'
    cutsat

end IsPrimitiveRoot

end NumberField.InfinitePlace

/-!

## The infinite place of the rationals.

-/

namespace Rat

open NumberField

/-- The infinite place of `‚Ñö`, coming from the canonical map `‚Ñö ‚Üí ‚ÑÇ`. -/
noncomputable def infinitePlace : InfinitePlace ‚Ñö := .mk (Rat.castHom _)

@[simp]
lemma infinitePlace_apply (v : InfinitePlace ‚Ñö) (x : ‚Ñö) : v x = |x| := by
  rw [NumberField.InfinitePlace.coe_apply]
  obtain ‚ü®_, _, rfl‚ü© := v
  simp

instance : Subsingleton (InfinitePlace ‚Ñö) where
  allEq a b := by ext; simp

lemma isReal_infinitePlace : InfinitePlace.IsReal (infinitePlace) :=
  ‚ü®Rat.castHom ‚ÑÇ, by ext; simp, rfl‚ü©

end Rat
