/-
Copyright (c) 2024 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.NumberTheory.NumberField.Basic
import Mathlib.RingTheory.FractionalIdeal.Norm
import Mathlib.RingTheory.FractionalIdeal.Operations

/-!

# Fractional ideals of number fields

Prove some results on the fractional ideals of number fields.

## Main definitions and results

  * `NumberField.basisOfFractionalIdeal`: A `‚Ñö`-basis of `K` that spans `I` over `‚Ñ§` where `I` is
  a fractional ideal of a number field `K`.
  * `NumberField.det_basisOfFractionalIdeal_eq_absNorm`: for `I` a fractional ideal of a number
  field `K`, the absolute value of the determinant of the base change from `integralBasis` to
  `basisOfFractionalIdeal I` is equal to the norm of `I`.
-/

variable (K : Type*) [Field K] [NumberField K]

namespace NumberField

open scoped nonZeroDivisors

section Basis

open Module

-- This is necessary to avoid several timeouts
attribute [local instance 2000] Submodule.module

instance (I : FractionalIdeal (ùìû K)‚Å∞ K) : Module.Free ‚Ñ§ I := by
  refine Free.of_equiv (LinearEquiv.restrictScalars ‚Ñ§ (I.equivNum ?_)).symm
  exact nonZeroDivisors.coe_ne_zero I.den

instance (I : FractionalIdeal (ùìû K)‚Å∞ K) : Module.Finite ‚Ñ§ I := by
  refine Module.Finite.of_surjective
    (LinearEquiv.restrictScalars ‚Ñ§ (I.equivNum ?_)).symm.toLinearMap (LinearEquiv.surjective _)
  exact nonZeroDivisors.coe_ne_zero I.den

instance (I : (FractionalIdeal (ùìû K)‚Å∞ K)À£) :
    IsLocalizedModule ‚Ñ§‚Å∞ ((Submodule.subtype (I : Submodule (ùìû K) K)).restrictScalars ‚Ñ§) where
  map_units x := by
    rw [‚Üê (Algebra.lmul _ _).commutes, Algebra.lmul_isUnit_iff, isUnit_iff_ne_zero, eq_intCast,
      Int.cast_ne_zero]
    exact nonZeroDivisors.coe_ne_zero x
  surj x := by
    obtain ‚ü®‚ü®a, _, d, hd, rfl‚ü©, h‚ü© := IsLocalization.surj (Algebra.algebraMapSubmonoid (ùìû K) ‚Ñ§‚Å∞) x
    refine ‚ü®‚ü®‚ü®Ideal.absNorm I.1.num * (algebraMap _ K a), I.1.num_le ?_‚ü©, d * Ideal.absNorm I.1.num,
      ?_‚ü©, ?_‚ü©
    ¬∑ refine (IsLocalization.mem_coeSubmodule _ _).mpr ‚ü®Ideal.absNorm I.1.num * a, ?_, ?_‚ü©
      ¬∑ exact Ideal.mul_mem_right _ _ I.1.num.absNorm_mem
      ¬∑ rw [map_mul, map_natCast]
    ¬∑ refine Submonoid.mul_mem _ hd (mem_nonZeroDivisors_of_ne_zero ?_)
      rw [Nat.cast_ne_zero, ne_eq, Ideal.absNorm_eq_zero_iff]
      exact FractionalIdeal.num_eq_zero_iff.not.mpr <| Units.ne_zero I
    ¬∑ simp_rw [LinearMap.coe_restrictScalars, Submodule.coe_subtype] at h ‚ä¢
      rw [‚Üê h]
      simp only [Submonoid.mk_smul, zsmul_eq_mul, Int.cast_mul, Int.cast_natCast, algebraMap_int_eq,
        eq_intCast, map_intCast]
      ring
  exists_of_eq h :=
    ‚ü®1, by rwa [one_smul, one_smul, ‚Üê (Submodule.injective_subtype I.1.coeToSubmodule).eq_iff]‚ü©

/-- A `‚Ñ§`-basis of a fractional ideal. -/
noncomputable def fractionalIdealBasis (I : FractionalIdeal (ùìû K)‚Å∞ K) :
    Basis (Free.ChooseBasisIndex ‚Ñ§ I) ‚Ñ§ I := Free.chooseBasis ‚Ñ§ I

/-- A `‚Ñö`-basis of `K` that spans `I` over `‚Ñ§`, see `mem_span_basisOfFractionalIdeal` below. -/
noncomputable def basisOfFractionalIdeal (I : (FractionalIdeal (ùìû K)‚Å∞ K)À£) :
    Basis (Free.ChooseBasisIndex ‚Ñ§ I) ‚Ñö K :=
  (fractionalIdealBasis K I.1).ofIsLocalizedModule ‚Ñö ‚Ñ§‚Å∞
    ((Submodule.subtype (I : Submodule (ùìû K) K)).restrictScalars ‚Ñ§)

theorem basisOfFractionalIdeal_apply (I : (FractionalIdeal (ùìû K)‚Å∞ K)À£)
    (i : Free.ChooseBasisIndex ‚Ñ§ I) :
    basisOfFractionalIdeal K I i = fractionalIdealBasis K I.1 i :=
  (fractionalIdealBasis K I.1).ofIsLocalizedModule_apply ‚Ñö ‚Ñ§‚Å∞ _ i

theorem mem_span_basisOfFractionalIdeal {I : (FractionalIdeal (ùìû K)‚Å∞ K)À£} {x : K} :
    x ‚àà Submodule.span ‚Ñ§ (Set.range (basisOfFractionalIdeal K I)) ‚Üî x ‚àà (I : Set K) := by
  rw [basisOfFractionalIdeal, (fractionalIdealBasis K I.1).ofIsLocalizedModule_span ‚Ñö ‚Ñ§‚Å∞ _]
  simp

open Module in
theorem fractionalIdeal_rank (I : (FractionalIdeal (ùìû K)‚Å∞ K)À£) :
    finrank ‚Ñ§ I = finrank ‚Ñ§ (ùìû K) := by
  rw [finrank_eq_card_chooseBasisIndex, RingOfIntegers.rank,
    finrank_eq_card_basis (basisOfFractionalIdeal K I)]

end Basis

section Norm

open Module

/-- The absolute value of the determinant of the base change from `integralBasis` to
`basisOfFractionalIdeal I` is equal to the norm of `I`. -/
theorem det_basisOfFractionalIdeal_eq_absNorm (I : (FractionalIdeal (ùìû K)‚Å∞ K)À£)
    (e : (Free.ChooseBasisIndex ‚Ñ§ (ùìû K)) ‚âÉ (Free.ChooseBasisIndex ‚Ñ§ I)) :
    |(integralBasis K).det ((basisOfFractionalIdeal K I).reindex e.symm)| =
      FractionalIdeal.absNorm I.1 := by
  rw [‚Üê FractionalIdeal.abs_det_basis_change (RingOfIntegers.basis K) I.1
    ((fractionalIdealBasis K I.1).reindex e.symm)]
  congr
  ext
  simpa using basisOfFractionalIdeal_apply K I _

end Norm

end NumberField
