/-
Copyright (c) 2022 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.Algebra.Module.Zlattice
import Mathlib.MeasureTheory.Group.GeometryOfNumbers
import Mathlib.MeasureTheory.Measure.Lebesgue.VolumeOfBalls
import Mathlib.NumberTheory.NumberField.Embeddings
import Mathlib.RingTheory.Discriminant

import Mathlib.Sandbox


#align_import number_theory.number_field.canonical_embedding from "leanprover-community/mathlib"@"60da01b41bbe4206f05d34fd70c8dd7498717a30"

/-!
# Canonical embedding of a number field

The canonical embedding of a number field `K` of degree `n` is the ring homomorphism
`K â†’+* â„‚^n` that sends `x âˆˆ K` to `(Ï†_â‚(x),...,Ï†_n(x))` where the `Ï†_i`'s are the complex
embeddings of `K`. Note that we do not choose an ordering of the embeddings, but instead map `K`
into the type `(K â†’+* â„‚) â†’ â„‚` of `â„‚`-vectors indexed by the complex embeddings.

## Main definitions and results

* `NumberField.canonicalEmbedding`: the ring homorphism `K â†’+* ((K â†’+* â„‚) â†’ â„‚)` defined by
sending `x : K` to the vector `(Ï† x)` indexed by `Ï† : K â†’+* â„‚`.

* `NumberField.canonicalEmbedding.integerLattice.inter_ball_finite`: the intersection of the
image of the ring of integers by the canonical embedding and any ball centered at `0` of finite
radius is finite.

* `NumberField.mixedEmbedding`: the ring homomorphism from `K â†’+* ({ w // IsReal w } â†’ â„) Ã—
({ w // IsComplex w } â†’ â„‚)` that sends `x âˆˆ K` to `(Ï†_w x)_w` where `Ï†_w` is the embedding
associated to the infinite place `w`. In particular, if `w` is real then `Ï†_w : K â†’+* â„` and, if
`w` is complex, `Ï†_w` is an arbitrary choice between the two complex embeddings defining the place
`w`.

* `NumberField.mixedEmbedding.exists_ne_zero_mem_ringOfIntegers_lt`: let
`f : InfinitePlace K â†’ â„â‰¥0`, if the product `âˆ w, f w` is large enough, then there exists a
nonzero algebraic integer `a` in `K` such that `w a < f w` for all infinite places `w`.

## Tags

number field, infinite places
-/

variable (K : Type*) [Field K]

namespace NumberField.canonicalEmbedding

open NumberField

/-- The canonical embedding of a number field `K` of degree `n` into `â„‚^n`. -/
def _root_.NumberField.canonicalEmbedding : K â†’+* ((K â†’+* â„‚) â†’ â„‚) := Pi.ringHom fun Ï† => Ï†

theorem _root_.NumberField.canonicalEmbedding_injective [NumberField K] :
    Function.Injective (NumberField.canonicalEmbedding K) := RingHom.injective _

variable {K}

@[simp]
theorem apply_at (Ï† : K â†’+* â„‚) (x : K) : (NumberField.canonicalEmbedding K x) Ï† = Ï† x := rfl

open scoped ComplexConjugate

/-- The image of `canonicalEmbedding` lives in the `â„`-submodule of the `x âˆˆ ((K â†’+* â„‚) â†’ â„‚)` such
that `conj x_Ï† = x_(conj Ï†)` for all `âˆ€ Ï† : K â†’+* â„‚`. -/
theorem conj_apply {x : ((K â†’+* â„‚) â†’ â„‚)} (Ï† : K â†’+* â„‚)
    (hx : x âˆˆ Submodule.span â„ (Set.range (canonicalEmbedding K))) :
    conj (x Ï†) = x (ComplexEmbedding.conjugate Ï†) := by
  refine Submodule.span_induction hx ?_ ?_ (fun _ _ hx hy => ?_) (fun a _ hx => ?_)
  Â· rintro _ âŸ¨x, rflâŸ©
    rw [apply_at, apply_at, ComplexEmbedding.conjugate_coe_eq]
  Â· rw [Pi.zero_apply, Pi.zero_apply, map_zero]
  Â· rw [Pi.add_apply, Pi.add_apply, map_add, hx, hy]
  Â· rw [Pi.smul_apply, Complex.real_smul, map_mul, Complex.conj_ofReal]
    exact congrArg ((a : â„‚) * Â·) hx

theorem nnnorm_eq [NumberField K] (x : K) :
    â€–canonicalEmbedding K xâ€–â‚Š = Finset.univ.sup (fun Ï† : K â†’+* â„‚ => â€–Ï† xâ€–â‚Š) := by
  simp_rw [Pi.nnnorm_def, apply_at]

theorem norm_le_iff [NumberField K] (x : K) (r : â„) :
    â€–canonicalEmbedding K xâ€– â‰¤ r â†” âˆ€ Ï† : K â†’+* â„‚, â€–Ï† xâ€– â‰¤ r := by
  obtain hr | hr := lt_or_le r 0
  Â· obtain âŸ¨Ï†âŸ© := (inferInstance : Nonempty (K â†’+* â„‚))
    refine iff_of_false ?_ ?_
    exact (hr.trans_le (norm_nonneg _)).not_le
    exact fun h => hr.not_le (le_trans (norm_nonneg _) (h Ï†))
  Â· lift r to NNReal using hr
    simp_rw [â† coe_nnnorm, nnnorm_eq, NNReal.coe_le_coe, Finset.sup_le_iff, Finset.mem_univ,
      forall_true_left]

variable (K)

/-- The image of `ğ“ K` as a subring of `â„‚^n`. -/
def integerLattice : Subring ((K â†’+* â„‚) â†’ â„‚) :=
  (RingHom.range (algebraMap (ğ“ K) K)).map (canonicalEmbedding K)

theorem integerLattice.inter_ball_finite [NumberField K] (r : â„) :
    ((integerLattice K : Set ((K â†’+* â„‚) â†’ â„‚)) âˆ© Metric.closedBall 0 r).Finite := by
  obtain hr | _ := lt_or_le r 0
  Â· simp [Metric.closedBall_eq_empty.2 hr]
  Â· have heq : âˆ€ x, canonicalEmbedding K x âˆˆ Metric.closedBall 0 r â†”
        âˆ€ Ï† : K â†’+* â„‚, â€–Ï† xâ€– â‰¤ r := by
      intro x; rw [â† norm_le_iff, mem_closedBall_zero_iff]
    convert (Embeddings.finite_of_norm_le K â„‚ r).image (canonicalEmbedding K)
    ext; constructor
    Â· rintro âŸ¨âŸ¨_, âŸ¨x, rflâŸ©, rflâŸ©, hxâŸ©
      exact âŸ¨â†‘x, âŸ¨SetLike.coe_mem x, fun Ï† => (heq x).mp hx Ï†âŸ©, rflâŸ©
    Â· rintro âŸ¨x, âŸ¨hx1, hx2âŸ©, rflâŸ©
      exact âŸ¨âŸ¨x, âŸ¨âŸ¨x, hx1âŸ©, rflâŸ©, rflâŸ©, (heq x).mpr hx2âŸ©

open Module Fintype FiniteDimensional

/-- A `â„‚`-basis of `â„‚^n` that is also a `â„¤`-basis of the `integerLattice`. -/
noncomputable def latticeBasis [NumberField K] :
    Basis (Free.ChooseBasisIndex â„¤ (ğ“ K)) â„‚ ((K â†’+* â„‚) â†’ â„‚) := by
  classical
  -- Let `B` be the canonical basis of `(K â†’+* â„‚) â†’ â„‚`. We prove that the determinant of
  -- the image by `canonicalEmbedding` of the integral basis of `K` is nonzero. This
  -- will imply the result.
    let B := Pi.basisFun â„‚ (K â†’+* â„‚)
    let e : (K â†’+* â„‚) â‰ƒ Free.ChooseBasisIndex â„¤ (ğ“ K) :=
      equivOfCardEq ((Embeddings.card K â„‚).trans (finrank_eq_card_basis (integralBasis K)))
    let M := B.toMatrix (fun i => canonicalEmbedding K (integralBasis K (e i)))
    suffices M.det â‰  0 by
      rw [â† isUnit_iff_ne_zero, â† Basis.det_apply, â† is_basis_iff_det] at this
      refine basisOfLinearIndependentOfCardEqFinrank
        ((linearIndependent_equiv e.symm).mpr this.1) ?_
      rw [â† finrank_eq_card_chooseBasisIndex, RingOfIntegers.rank, finrank_fintype_fun_eq_card,
        Embeddings.card]
  -- In order to prove that the determinant is nonzero, we show that it is equal to the
  -- square of the discriminant of the integral basis and thus it is not zero
    let N := Algebra.embeddingsMatrixReindex â„š â„‚ (fun i => integralBasis K (e i))
      RingHom.equivRatAlgHom
    rw [show M = N.transpose by { ext:2; rfl }]
    rw [Matrix.det_transpose, â† @pow_ne_zero_iff â„‚ _ _ _ 2 (by norm_num)]
    convert (map_ne_zero_iff _ (algebraMap â„š â„‚).injective).mpr
      (Algebra.discr_not_zero_of_basis â„š (integralBasis K))
    rw [â† Algebra.discr_reindex â„š (integralBasis K) e.symm]
    exact (Algebra.discr_eq_det_embeddingsMatrixReindex_pow_two â„š â„‚
      (fun i => integralBasis K (e i)) RingHom.equivRatAlgHom).symm

@[simp]
theorem latticeBasis_apply [NumberField K] (i : Free.ChooseBasisIndex â„¤ (ğ“ K)) :
    latticeBasis K i = (canonicalEmbedding K) (integralBasis K i) := by
  simp only [latticeBasis, integralBasis_apply, coe_basisOfLinearIndependentOfCardEqFinrank,
    Function.comp_apply, Equiv.apply_symm_apply]

theorem mem_span_latticeBasis [NumberField K] (x : (K â†’+* â„‚) â†’ â„‚) :
    x âˆˆ Submodule.span â„¤ (Set.range (latticeBasis K)) â†” x âˆˆ canonicalEmbedding K '' (ğ“ K) := by
  rw [show Set.range (latticeBasis K) =
      (canonicalEmbedding K).toIntAlgHom.toLinearMap '' (Set.range (integralBasis K)) by
    rw [â† Set.range_comp]; exact congrArg Set.range (funext (fun i => latticeBasis_apply K i))]
  rw [â† Submodule.map_span, â† SetLike.mem_coe, Submodule.map_coe]
  rw [show (Submodule.span â„¤ (Set.range (integralBasis K)) : Set K) = ğ“ K by
    ext; exact mem_span_integralBasis K]
  rfl

end NumberField.canonicalEmbedding

namespace NumberField.mixedEmbedding

open NumberField NumberField.InfinitePlace FiniteDimensional

/-- The space `â„^râ‚ Ã— â„‚^râ‚‚` with `(râ‚, râ‚‚)` the signature of `K`. -/
local notation "E" K =>
  ({w : InfinitePlace K // IsReal w} â†’ â„) Ã— ({w : InfinitePlace K // IsComplex w} â†’ â„‚)

/-- The mixed embedding of a number field `K` of signature `(râ‚, râ‚‚)` into `â„^râ‚ Ã— â„‚^râ‚‚`. -/
noncomputable def _root_.NumberField.mixedEmbedding : K â†’+* (E K) :=
  RingHom.prod (Pi.ringHom fun w => embedding_of_isReal w.prop)
    (Pi.ringHom fun w => w.val.embedding)

instance [NumberField K] : Nontrivial (E K) := by
  obtain âŸ¨wâŸ© := (inferInstance : Nonempty (InfinitePlace K))
  obtain hw | hw := w.isReal_or_isComplex
  Â· have : Nonempty {w : InfinitePlace K // IsReal w} := âŸ¨âŸ¨w, hwâŸ©âŸ©
    exact nontrivial_prod_left
  Â· have : Nonempty {w : InfinitePlace K // IsComplex w} := âŸ¨âŸ¨w, hwâŸ©âŸ©
    exact nontrivial_prod_right

protected theorem finrank [NumberField K] : finrank â„ (E K) = finrank â„š K := by
  classical
  rw [finrank_prod, finrank_pi, finrank_pi_fintype, Complex.finrank_real_complex, Finset.sum_const,
    Finset.card_univ, â† NrRealPlaces, â† NrComplexPlaces, â† card_real_embeddings,
    Algebra.id.smul_eq_mul, mul_comm, â† card_complex_embeddings, â† NumberField.Embeddings.card K â„‚,
    Fintype.card_subtype_compl, Nat.add_sub_of_le (Fintype.card_subtype_le _)]

theorem _root_.NumberField.mixedEmbedding_injective [NumberField K] :
    Function.Injective (NumberField.mixedEmbedding K) := by
  exact RingHom.injective _

section commMap

/-- The linear map that makes `canonicalEmbedding` and `mixedEmbedding` commute, see
`commMap_canonical_eq_mixed`. -/
noncomputable def commMap : ((K â†’+* â„‚) â†’ â„‚) â†’â‚—[â„] (E K) where
  toFun := fun x => âŸ¨fun w => (x w.val.embedding).re, fun w => x w.val.embeddingâŸ©
  map_add' := by
    simp only [Pi.add_apply, Complex.add_re, Prod.mk_add_mk, Prod.mk.injEq]
    exact fun _ _ => âŸ¨rfl, rflâŸ©
  map_smul' := by
    simp only [Pi.smul_apply, Complex.real_smul, Complex.mul_re, Complex.ofReal_re,
      Complex.ofReal_im, zero_mul, sub_zero, RingHom.id_apply, Prod.smul_mk, Prod.mk.injEq]
    exact fun _ _ => âŸ¨rfl, rflâŸ©

theorem commMap_apply_of_isReal (x : (K â†’+* â„‚) â†’ â„‚) {w : InfinitePlace K} (hw : IsReal w) :
    (commMap K x).1 âŸ¨w, hwâŸ© = (x w.embedding).re := rfl

theorem commMap_apply_of_isComplex (x : (K â†’+* â„‚) â†’ â„‚) {w : InfinitePlace K} (hw : IsComplex w) :
    (commMap K x).2 âŸ¨w, hwâŸ© = x w.embedding := rfl

@[simp]
theorem commMap_canonical_eq_mixed (x : K) :
    commMap K (canonicalEmbedding K x) = mixedEmbedding K x := by
  simp only [canonicalEmbedding, commMap, LinearMap.coe_mk, AddHom.coe_mk, Pi.ringHom_apply,
    mixedEmbedding, RingHom.prod_apply, Prod.mk.injEq]
  exact âŸ¨rfl, rflâŸ©

/-- This is a technical result to ensure that the image of the `â„‚`-basis of `â„‚^n` defined in
`canonicalEmbedding.latticeBasis` is a `â„`-basis of `â„^râ‚ Ã— â„‚^râ‚‚`,
see `mixedEmbedding.latticeBasis`. -/
theorem disjoint_span_commMap_ker [NumberField K] :
    Disjoint (Submodule.span â„ (Set.range (canonicalEmbedding.latticeBasis K)))
      (LinearMap.ker (commMap K)) := by
  refine LinearMap.disjoint_ker.mpr (fun x h_mem h_zero => ?_)
  replace h_mem : x âˆˆ Submodule.span â„ (Set.range (canonicalEmbedding K)) := by
    refine (Submodule.span_mono ?_) h_mem
    rintro _ âŸ¨i, rflâŸ©
    exact âŸ¨integralBasis K i, (canonicalEmbedding.latticeBasis_apply K i).symmâŸ©
  ext1 Ï†
  rw [Pi.zero_apply]
  by_cases hÏ† : ComplexEmbedding.IsReal Ï†
  Â· apply Complex.ext
    Â· rw [â† embedding_mk_eq_of_isReal hÏ†, â† commMap_apply_of_isReal K x âŸ¨Ï†, hÏ†, rflâŸ©]
      exact congrFun (congrArg (fun x => x.1) h_zero) âŸ¨InfinitePlace.mk Ï†, _âŸ©
    Â· rw [Complex.zero_im, â† Complex.conj_eq_iff_im, canonicalEmbedding.conj_apply _ h_mem,
        ComplexEmbedding.isReal_iff.mp hÏ†]
  Â· have := congrFun (congrArg (fun x => x.2) h_zero) âŸ¨InfinitePlace.mk Ï†, âŸ¨Ï†, hÏ†, rflâŸ©âŸ©
    cases embedding_mk_eq Ï† with
    | inl h => rwa [â† h, â† commMap_apply_of_isComplex K x âŸ¨Ï†, hÏ†, rflâŸ©]
    | inr h =>
        apply RingHom.injective (starRingEnd â„‚)
        rwa [canonicalEmbedding.conj_apply _ h_mem, â† h, map_zero,
          â† commMap_apply_of_isComplex K x âŸ¨Ï†, hÏ†, rflâŸ©]

end commMap

noncomputable section stdBasis

open Classical Complex MeasureTheory MeasureTheory.Measure Zspan Matrix BigOperators
  ComplexConjugate

variable [NumberField K]

/-- The type indexing the basis `stdBasis`. -/
abbrev index := {w : InfinitePlace K // IsReal w} âŠ• ({w : InfinitePlace K // IsComplex w}) Ã— (Fin 2)

/-- The `â„`-basis of `({w // IsReal w} â†’ â„) Ã— ({ w // IsComplex w } â†’ â„‚)` formed by the vector
equal to `1` at `w` and `0` elsewhere for `IsReal w` and by the couple of vectors equal to `1`
(resp. `I`) at `w` and `0` elsewhere for `IsComplex w`. -/
def stdBasis : Basis (index K) â„ (E K) :=
  Basis.prod (Pi.basisFun â„ _)
    (Basis.reindex (Pi.basis fun _ => basisOneI) (Equiv.sigmaEquivProd _ _))

variable {K}

@[simp]
theorem stdBasis_apply_ofIsReal (x : E K) (w : {w : InfinitePlace K // IsReal w}) :
    (stdBasis K).repr x (Sum.inl w) = x.1 w := rfl

@[simp]
theorem stdBasis_apply_ofIsComplex_fst (x : E K) (w : {w : InfinitePlace K // IsComplex w}) :
    (stdBasis K).repr x (Sum.inr âŸ¨w, 0âŸ©) = (x.2 w).re := rfl

@[simp]
theorem stdBasis_apply_ofIsComplex_snd (x : E K) (w : {w : InfinitePlace K // IsComplex w}) :
    (stdBasis K).repr x (Sum.inr âŸ¨w, 1âŸ©) = (x.2 w).im := rfl

variable (K)

theorem fundamentalDomain_stdBasis :
    fundamentalDomain (stdBasis K) =
        (Set.univ.pi fun _ => Set.Ico 0 1) Ã—Ë¢
        (Set.univ.pi fun _ => Complex.measurableEquivPiâ»Â¹' (Set.univ.pi fun _ => Set.Ico 0 1)) := by
  ext
  simp [stdBasis, mem_fundamentalDomain, Complex.measurableEquivPi]

theorem volume_fundamentalDomain_stdBasis :
    volume (fundamentalDomain (stdBasis K)) = 1 := by
  rw [fundamentalDomain_stdBasis, volume_eq_prod, prod_prod, volume_pi, volume_pi, pi_pi, pi_pi,
    Complex.volume_preserving_equiv_pi.measure_preimage ?_, volume_pi, pi_pi, Real.volume_Ico,
    sub_zero, ENNReal.ofReal_one, Finset.prod_const_one, Finset.prod_const_one,
    Finset.prod_const_one, one_mul]
  exact MeasurableSet.pi Set.countable_univ (fun _ _ => measurableSet_Ico)

/-- The `Equiv` between `index K` and `K â†’+* â„‚` defined by sending a real infinite place `w` to
the unique corresponding embedding `w.embedding`, and the pair `âŸ¨w, 0âŸ©` (resp. `âŸ¨w, 1âŸ©`) for a
complex infinite place `w` to `w.embedding` (resp. `conjugate w.embedding`). -/
def indexEquiv : (index K) â‰ƒ (K â†’+* â„‚) := by
  refine Equiv.ofBijective (fun c => ?_)
    ((Fintype.bijective_iff_surjective_and_card _).mpr âŸ¨?_, ?_âŸ©)
  Â· cases c with
    | inl w => exact w.val.embedding
    | inr wj => rcases wj with âŸ¨w, jâŸ©
                exact if j = 0 then w.val.embedding else ComplexEmbedding.conjugate w.val.embedding
  Â· intro Ï†
    by_cases hÏ† : ComplexEmbedding.IsReal Ï†
    Â· exact âŸ¨Sum.inl (InfinitePlace.mkReal âŸ¨Ï†, hÏ†âŸ©), by simp [embedding_mk_eq_of_isReal hÏ†]âŸ©
    Â· by_cases hw : (InfinitePlace.mk Ï†).embedding = Ï†
      Â· exact âŸ¨Sum.inr âŸ¨InfinitePlace.mkComplex âŸ¨Ï†, hÏ†âŸ©, 0âŸ©, by simp [hw]âŸ©
      Â· exact âŸ¨Sum.inr âŸ¨InfinitePlace.mkComplex âŸ¨Ï†, hÏ†âŸ©, 1âŸ©,
          by simp [(embedding_mk_eq Ï†).resolve_left hw]âŸ©
  Â· rw [Embeddings.card, â† mixedEmbedding.finrank K,
      â† FiniteDimensional.finrank_eq_card_basis (stdBasis K)]

variable {K}

@[simp]
theorem indexEquiv_apply_ofIsReal (w : {w : InfinitePlace K // IsReal w}) :
    (indexEquiv K) (Sum.inl w) = w.val.embedding := rfl

@[simp]
theorem indexEquiv_apply_ofIsComplex_fst (w : {w : InfinitePlace K // IsComplex w}) :
    (indexEquiv K) (Sum.inr âŸ¨w, 0âŸ©) = w.val.embedding := rfl

@[simp]
theorem indexEquiv_apply_ofIsComplex_snd (w : {w : InfinitePlace K // IsComplex w}) :
    (indexEquiv K) (Sum.inr âŸ¨w, 1âŸ©) = ComplexEmbedding.conjugate w.val.embedding := rfl

variable (K)

/-- The matrix that gives the representation on `stdBasis` of the image by `commMap` of an
element `x` of `(K â†’+* â„‚) â†’ â„‚` fixed by the map `x_Ï† â†¦ conj x_(conjugate Ï†)`,
see `stdBasis_repr_eq_matrixToStdBasis_mul`. -/
def matrixToStdBasis : Matrix (index K) (index K) â„‚ :=
  fromBlocks (diagonal fun _ => 1) 0 0 <| reindex (Equiv.prodComm _ _) (Equiv.prodComm _ _)
    (blockDiagonal (fun _ => (2 : â„‚)â»Â¹ â€¢ !![1, 1; - I, I]))

theorem det_matrixToStdBasis :
    (matrixToStdBasis K).det = (2â»Â¹ * I) ^ NrComplexPlaces K :=
  calc
  _ = âˆ _k : { w : InfinitePlace K // IsComplex w }, det ((2 : â„‚)â»Â¹ â€¢ !![1, 1; -I, I]) := by
      rw [matrixToStdBasis, det_fromBlocks_zeroâ‚‚â‚, det_diagonal, Finset.prod_const_one, one_mul,
          det_reindex_self, det_blockDiagonal]
  _ = âˆ _k : { w : InfinitePlace K // IsComplex w }, (2â»Â¹ * Complex.I) := by
      refine Finset.prod_congr (Eq.refl _) (fun _ _ => ?_)
      field_simp; ring
  _ = (2â»Â¹ * Complex.I) ^ Fintype.card {w : InfinitePlace K // IsComplex w} := by
      rw [Finset.prod_const, Fintype.card]

/-- Let `x : (K â†’+* â„‚) â†’ â„‚` such that `x_Ï† = conj x_(conj Ï†)` for all `Ï† : K â†’+* â„‚`, then the
representation of `commMap K x` on `stdBasis` is given (up to reindexing) by the product of
`matrixToStdBasis` by `x`. -/
theorem stdBasis_repr_eq_matrixToStdBasis_mul (x : (K â†’+* â„‚) â†’ â„‚)
    (hx : âˆ€ Ï†, conj (x Ï†) = x (ComplexEmbedding.conjugate Ï†)) (c : index K) :
    ((stdBasis K).repr (commMap K x) c : â„‚) =
      (mulVec (matrixToStdBasis K) (x âˆ˜ (indexEquiv K))) c := by
  simp_rw [commMap, matrixToStdBasis, LinearMap.coe_mk, AddHom.coe_mk,
    mulVec, dotProduct, Function.comp_apply, index, Fintype.sum_sum_type,
    diagonal_one, reindex_apply, â† Finset.univ_product_univ, Finset.sum_product,
    indexEquiv_apply_ofIsReal, Fin.sum_univ_two, indexEquiv_apply_ofIsComplex_fst,
    indexEquiv_apply_ofIsComplex_snd, smul_of, smul_cons, smul_eq_mul,
    mul_one, smul_empty, Equiv.prodComm_symm, Equiv.coe_prodComm]
  cases c with
  | inl w =>
      simp_rw [stdBasis_apply_ofIsReal, fromBlocks_applyâ‚â‚, fromBlocks_applyâ‚â‚‚,
        one_apply, Matrix.zero_apply, ite_mul, one_mul, zero_mul, Finset.sum_ite_eq,
        Finset.mem_univ, ite_true, add_zero, Finset.sum_const_zero, add_zero,
        â† conj_eq_iff_re, hx (embedding w.val), conjugate_embedding_eq_of_isReal w.prop]
  | inr c =>
    rcases c with âŸ¨w, jâŸ©
    fin_cases j
    Â· simp_rw [Fin.mk_zero, stdBasis_apply_ofIsComplex_fst, fromBlocks_applyâ‚‚â‚,
        fromBlocks_applyâ‚‚â‚‚, Matrix.zero_apply, submatrix_apply,
        blockDiagonal_apply, Prod.swap_prod_mk, ite_mul, zero_mul, Finset.sum_const_zero,
        zero_add, Finset.sum_add_distrib, Finset.sum_ite_eq, Finset.mem_univ, ite_true,
        of_apply, cons_val', cons_val_zero, cons_val_one,
        head_cons, â† hx (embedding w), re_eq_add_conj]
      field_simp
    Â· simp_rw [Fin.mk_one, stdBasis_apply_ofIsComplex_snd, fromBlocks_applyâ‚‚â‚,
        fromBlocks_applyâ‚‚â‚‚, Matrix.zero_apply, submatrix_apply,
        blockDiagonal_apply, Prod.swap_prod_mk, ite_mul, zero_mul, Finset.sum_const_zero,
        zero_add, Finset.sum_add_distrib, Finset.sum_ite_eq, Finset.mem_univ, ite_true,
        of_apply, cons_val', cons_val_zero, cons_val_one,
        head_cons, â† hx (embedding w), im_eq_sub_conj]
      ring_nf; field_simp

end stdBasis

noncomputable section integerLattice

variable [NumberField K]

open Module FiniteDimensional Module.Free

open scoped nonZeroDivisors

/-- A `â„`-basis of `â„^râ‚ Ã— â„‚^râ‚‚` that is also a `â„¤`-basis of the image of `ğ“ K`. -/
def latticeBasis :
    Basis (ChooseBasisIndex â„¤ (ğ“ K)) â„ (E K) := by
  classical
    -- We construct an `â„`-linear independent family from the image of
    -- `canonicalEmbedding.lattice_basis` by `commMap`
    have := LinearIndependent.map (LinearIndependent.restrict_scalars
      (by { simpa only [Complex.real_smul, mul_one] using Complex.ofReal_injective })
      (canonicalEmbedding.latticeBasis K).linearIndependent)
      (disjoint_span_commMap_ker K)
    -- and it's a basis since it has the right cardinality
    refine basisOfLinearIndependentOfCardEqFinrank this ?_
    rw [â† finrank_eq_card_chooseBasisIndex, RingOfIntegers.rank, finrank_prod, finrank_pi,
      finrank_pi_fintype, Complex.finrank_real_complex, Finset.sum_const, Finset.card_univ,
      â† NrRealPlaces, â† NrComplexPlaces, â† card_real_embeddings, Algebra.id.smul_eq_mul, mul_comm,
      â† card_complex_embeddings, â† NumberField.Embeddings.card K â„‚, Fintype.card_subtype_compl,
      Nat.add_sub_of_le (Fintype.card_subtype_le _)]

@[simp]
theorem latticeBasis_apply (i : ChooseBasisIndex â„¤ (ğ“ K)) :
    latticeBasis K i = (mixedEmbedding K) (integralBasis K i) := by
  simp only [latticeBasis, coe_basisOfLinearIndependentOfCardEqFinrank, Function.comp_apply,
    canonicalEmbedding.latticeBasis_apply, integralBasis_apply, commMap_canonical_eq_mixed]

theorem mem_span_latticeBasis (x : (E K)) :
    x âˆˆ Submodule.span â„¤ (Set.range (latticeBasis K)) â†” x âˆˆ mixedEmbedding K '' (ğ“ K) := by
  rw [show Set.range (latticeBasis K) =
      (mixedEmbedding K).toIntAlgHom.toLinearMap '' (Set.range (integralBasis K)) by
    rw [â† Set.range_comp]; exact congrArg Set.range (funext (fun i => latticeBasis_apply K i))]
  rw [â† Submodule.map_span, â† SetLike.mem_coe, Submodule.map_coe]
  rw [show (Submodule.span â„¤ (Set.range (integralBasis K)) : Set K) = ğ“ K by
    ext; exact mem_span_integralBasis K]
  rfl

theorem mem_rat_span_latticeBasis (x : K) :
    mixedEmbedding K x âˆˆ Submodule.span â„š (Set.range (latticeBasis K)) := by
  rw [â† Basis.sum_repr (integralBasis K) x, map_sum]
  simp_rw [map_rat_smul]
  refine Submodule.sum_smul_mem _ _ (fun i _ â†¦ Submodule.subset_span ?_)
  rw [â† latticeBasis_apply]
  exact Set.mem_range_self i

variable (I : (Ideal (ğ“ K))â°)

theorem latticeBasis_repr_apply (x : K) (i : ChooseBasisIndex â„¤ (ğ“ K)) :
    (latticeBasis K).repr (mixedEmbedding K x) i = (integralBasis K).repr x i := by
  rw [â† Basis.restrictScalars_repr_apply â„š _ âŸ¨_, mem_rat_span_latticeBasis K xâŸ©, eq_ratCast,
    Rat.cast_inj]
  let f := (mixedEmbedding K).toRatAlgHom.toLinearMap.codRestrict _
    (fun x â†¦ mem_rat_span_latticeBasis K x)
  suffices ((latticeBasis K).restrictScalars â„š).repr.toLinearMap âˆ˜â‚— f =
    (integralBasis K).repr.toLinearMap from FunLike.congr_fun (LinearMap.congr_fun this x) i
  refine Basis.ext (integralBasis K) (fun i â†¦ ?_)
  have : f (integralBasis K i) = ((latticeBasis K).restrictScalars â„š) i := by
    apply Subtype.val_injective
    rw [LinearMap.codRestrict_apply, AlgHom.toLinearMap_apply, Basis.restrictScalars_apply,
      latticeBasis_apply]
    rfl
  simp_rw [LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply, this, Basis.repr_self]

theorem det_idealBasis_eq_norm
    (e : (ChooseBasisIndex â„¤ (ğ“ K)) â‰ƒ (ChooseBasisIndex â„¤ I)) :
    |Basis.det (latticeBasis K) ((mixedEmbedding K âˆ˜ (idealBasis K I) âˆ˜ e))| =
      Ideal.absNorm (I : Ideal (ğ“ K)) := by
  have : Basis.det (latticeBasis K) ((mixedEmbedding K âˆ˜ (idealBasis K I) âˆ˜ e)) =
      (algebraMap â„š â„) ((Basis.det (integralBasis K)) ((idealBasis K I) âˆ˜ e)) := by
    rw [Basis.det_apply, Basis.det_apply, RingHom.map_det]
    congr
    ext i j
    simp_rw [RingHom.mapMatrix_apply, Matrix.map_apply, Basis.toMatrix_apply, Function.comp_apply]
    exact latticeBasis_repr_apply K _ i
  rw [this, eq_ratCast, â† Rat.cast_abs, â† Equiv.symm_symm e, â† Basis.coe_reindex,
    det_idealBasis_eq_ideal_absNorm K I e, Rat.cast_coe_nat]

/-- A `â„`-basis of `â„^râ‚ Ã— â„‚^râ‚‚` that is also a `â„¤`-basis of the image of the ideal `I`. -/
def idealLatticeBasis :
    Basis (ChooseBasisIndex â„¤ I) â„ (E K) := by
  let e : (ChooseBasisIndex â„¤ (ğ“ K)) â‰ƒ (ChooseBasisIndex â„¤ I) := by
    refine Fintype.equivOfCardEq ?_
    rw [â† finrank_eq_card_chooseBasisIndex, â† finrank_eq_card_chooseBasisIndex, ideal_rank_eq]
  refine Basis.reindex ?_ e
  suffices IsUnit ((latticeBasis K).det ((mixedEmbedding K) âˆ˜ (idealBasis K I) âˆ˜ e)) by
    rw [â† is_basis_iff_det] at this
    exact Basis.mk this.1 (by rw [this.2])
  rw [isUnit_iff_ne_zero, ne_eq, â† abs_eq_zero.not, det_idealBasis_eq_norm, Nat.cast_eq_zero,
    â† ne_eq]
  exact ideal_absNorm_ne_zero K I

@[simp]
theorem idealLatticeBasis_apply (i : ChooseBasisIndex â„¤ I) :
    idealLatticeBasis K I i = (mixedEmbedding K) (idealBasis K I i) := by
  simp only [idealLatticeBasis, Basis.coe_reindex, Basis.coe_mk, Function.comp_apply,
    Equiv.apply_symm_apply]

theorem mem_span_idealLatticeBasis (x : (E K)) :
    x âˆˆ Submodule.span â„¤ (Set.range (idealLatticeBasis K I)) â†”
      x âˆˆ mixedEmbedding K '' (I : Set (ğ“ K)) := by
  rw [show Set.range (idealLatticeBasis K I) =
      (mixedEmbedding K).toIntAlgHom.toLinearMap '' (Set.range (idealBasis K I)) by
    rw [â† Set.range_comp]
    exact congr_arg Set.range (funext (fun i â†¦ idealLatticeBasis_apply K I i))]
  rw [â† Submodule.map_span, â† SetLike.mem_coe, Submodule.map_coe]
  rw [show Submodule.span â„¤ (Set.range (idealBasis K I)) = algebraMap (ğ“ K) K '' (I : Ideal (ğ“ K))
    by ext; exact mem_span_idealBasis K I]
  rfl

end integerLattice

section convexBodyLT

open Metric ENNReal NNReal

variable (f : InfinitePlace K â†’ â„â‰¥0)

/-- The convex body defined by `f`: the set of points `x : E` such that `â€–x wâ€– < f w` for all
infinite places `w`. -/
abbrev convexBodyLT : Set (E K) :=
  (Set.univ.pi (fun w : { w : InfinitePlace K // IsReal w } => ball 0 (f w))) Ã—Ë¢
  (Set.univ.pi (fun w : { w : InfinitePlace K // IsComplex w } => ball 0 (f w)))

theorem convexBodyLT_mem {x : K} :
    mixedEmbedding K x âˆˆ (convexBodyLT K f) â†” âˆ€ w : InfinitePlace K, w x < f w := by
  simp_rw [mixedEmbedding, RingHom.prod_apply, Set.mem_prod, Set.mem_pi, Set.mem_univ,
    forall_true_left, mem_ball_zero_iff, Pi.ringHom_apply, â† Complex.norm_real,
    embedding_of_isReal_apply, Subtype.forall, â† ball_or_left, â† not_isReal_iff_isComplex, em,
    forall_true_left, norm_embedding_eq]

theorem convexBodyLT_symmetric (x : E K) (hx : x âˆˆ (convexBodyLT K f)) :
    -x âˆˆ (convexBodyLT K f) := by
  simp only [Set.mem_prod, Prod.fst_neg, Set.mem_pi, Set.mem_univ, Pi.neg_apply,
    mem_ball_zero_iff, norm_neg, Real.norm_eq_abs, forall_true_left, Subtype.forall,
    Prod.snd_neg, Complex.norm_eq_abs, hx] at hx âŠ¢
  exact hx

theorem convexBodyLT_convex : Convex â„ (convexBodyLT K f) :=
  Convex.prod (convex_pi (fun _ _ => convex_ball _ _)) (convex_pi (fun _ _ => convex_ball _ _))

open Classical Fintype MeasureTheory MeasureTheory.Measure BigOperators

variable [NumberField K]

instance : IsAddHaarMeasure (volume : Measure (E K)) := prod.instIsAddHaarMeasure volume volume

instance : NoAtoms (volume : Measure (E K)) := by
  obtain âŸ¨wâŸ© := (inferInstance : Nonempty (InfinitePlace K))
  by_cases hw : IsReal w
  exact @prod.instNoAtoms_fst _ _ _ _ volume volume _ (pi_noAtoms âŸ¨w, hwâŸ©)
  Â· exact @prod.instNoAtoms_snd _ _ _ _ volume volume _
      (pi_noAtoms âŸ¨w, not_isReal_iff_isComplex.mp hwâŸ©)

/-- The fudge factor that appears in the formula for the volume of `convexBodyLT`. -/
noncomputable abbrev convexBodyLTFactor : â„â‰¥0âˆ :=
  (2 : â„â‰¥0âˆ) ^ NrRealPlaces K * (NNReal.pi : â„â‰¥0âˆ) ^ NrComplexPlaces K

theorem convexBodyLTFactor_pos : 0 < (convexBodyLTFactor K) := by
  refine mul_pos (NeZero.ne _) (ENNReal.pow_ne_zero ?_ _)
  exact ne_of_gt (coe_pos.mpr Real.pi_pos)

theorem convexBodyLTFactor_lt_top : (convexBodyLTFactor K) < âŠ¤ := by
  refine mul_lt_top ?_ ?_
  Â· exact ne_of_lt (pow_lt_top (lt_top_iff_ne_top.mpr two_ne_top) _)
  Â· exact ne_of_lt (pow_lt_top coe_lt_top _)

/-- The volume of `(ConvexBodyLt K f)` where `convexBodyLT K f` is the set of points `x`
such that `â€–x wâ€– < f w` for all infinite places `w`. -/
theorem convexBodyLT_volume :
    volume (convexBodyLT K f) = (convexBodyLTFactor K) * âˆ w, (f w) ^ (mult w) := by
  calc
    _ = (âˆ x : {w // InfinitePlace.IsReal w}, ENNReal.ofReal (2 * (f x.val))) *
          âˆ x : {w // InfinitePlace.IsComplex w}, ENNReal.ofReal (f x.val) ^ 2 * pi := by
      simp_rw [volume_eq_prod, prod_prod, volume_pi, pi_pi, Real.volume_ball, Complex.volume_ball]
    _ = (â†‘2 ^ NrRealPlaces K * (âˆ x : {w // InfinitePlace.IsReal w}, ENNReal.ofReal (f x.val))) *
          ((âˆ x : {w // IsComplex w}, ENNReal.ofReal (f x.val) ^ 2) * â†‘pi ^ NrComplexPlaces K) := by
      simp_rw [ofReal_mul (by norm_num : 0 â‰¤ (2 : â„)), Finset.prod_mul_distrib, Finset.prod_const,
        Finset.card_univ, ofReal_ofNat]
    _ = (convexBodyLTFactor K) * ((âˆ x : {w // InfinitePlace.IsReal w}, ENNReal.ofReal (f x.val)) *
        (âˆ x : {w // IsComplex w}, ENNReal.ofReal (f x.val) ^ 2)) := by ring
    _ = (convexBodyLTFactor K) * âˆ w, (f w) ^ (mult w) := by
      simp_rw [mult, pow_ite, pow_one, Finset.prod_ite, ofReal_coe_nnreal, not_isReal_iff_isComplex,
        coe_mul, coe_finset_prod, ENNReal.coe_pow]
      congr 2
      Â· refine (Finset.prod_subtype (Finset.univ.filter _) ?_ (fun w => (f w : â„â‰¥0âˆ))).symm
        exact fun _ => by simp only [Finset.mem_univ, forall_true_left, Finset.mem_filter, true_and]
      Â· refine (Finset.prod_subtype (Finset.univ.filter _) ?_ (fun w => (f w : â„â‰¥0âˆ) ^ 2)).symm
        exact fun _ => by simp only [Finset.mem_univ, forall_true_left, Finset.mem_filter, true_and]

variable {f}

/-- This is a technical result: quite often, we want to impose conditions at all infinite places
but one and choose the value at the remaining place so that we can apply
`exists_ne_zero_mem_ringOfIntegers_lt`. -/
theorem adjust_f {wâ‚ : InfinitePlace K} (B : â„â‰¥0) (hf : âˆ€ w, w â‰  wâ‚ â†’ f w â‰  0) :
    âˆƒ g : InfinitePlace K â†’ â„â‰¥0, (âˆ€ w, w â‰  wâ‚ â†’ g w = f w) âˆ§ âˆ w, (g w) ^ mult w = B := by
  let S := âˆ w in Finset.univ.erase wâ‚, (f w) ^ mult w
  refine âŸ¨Function.update f wâ‚ ((B * Sâ»Â¹) ^ (mult wâ‚ : â„)â»Â¹), ?_, ?_âŸ©
  Â· exact fun w hw => Function.update_noteq hw _ f
  Â· rw [â† Finset.mul_prod_erase Finset.univ _ (Finset.mem_univ wâ‚), Function.update_same,
      Finset.prod_congr rfl fun w hw => by rw [Function.update_noteq (Finset.ne_of_mem_erase hw)],
      â† NNReal.rpow_nat_cast, â† NNReal.rpow_mul, inv_mul_cancel, NNReal.rpow_one, mul_assoc,
      inv_mul_cancel, mul_one]
    Â· rw [Finset.prod_ne_zero_iff]
      exact fun w hw => pow_ne_zero _ (hf w (Finset.ne_of_mem_erase hw))
    Â· rw [mult]; split_ifs <;> norm_num

end convexBodyLT

section convexBodySum

open ENNReal BigOperators Classical MeasureTheory Fintype

open scoped Real

variable [NumberField K] (B : â„)

variable {K}

/-- The function that sends `x : ({w // IsReal w} â†’ â„) Ã— ({w // IsComplex w} â†’ â„‚)` to
  `âˆ‘ w, â€–x.1 wâ€– + 2 * âˆ‘ w, â€–x.2 wâ€–`. It defines a norm and it used to define `convexBodySum`. -/
noncomputable abbrev convexBodySumFun (x : E K) : â„ := âˆ‘ w, â€–x.1 wâ€– + 2 * âˆ‘ w, â€–x.2 wâ€–

theorem convexBodySumFun_nonneg (x : E K) :
    0 â‰¤ convexBodySumFun x := by
  refine add_nonneg ?_ ?_
  Â· exact Finset.sum_nonneg (fun _ _ => norm_nonneg _)
  Â· exact mul_nonneg zero_le_two (Finset.sum_nonneg (fun _ _ => norm_nonneg _))

theorem convexBodySumFun_neg (x : E K) :
    convexBodySumFun (- x) = convexBodySumFun x := by
  simp_rw [convexBodySumFun, Prod.fst_neg, Prod.snd_neg, Pi.neg_apply, norm_neg]

theorem convexBodySumFun_add_le (x y : E K) :
    convexBodySumFun (x + y) â‰¤ convexBodySumFun x + convexBodySumFun y := by
  simp_rw [convexBodySumFun, Prod.fst_add, Pi.add_apply, Prod.snd_add]
  refine le_trans (add_le_add
    (Finset.sum_le_sum (fun w _ => norm_add_le (x.1 w) (y.1 w)))
    (mul_le_mul_of_nonneg_left
      (Finset.sum_le_sum (fun w _ => norm_add_le (x.2 w) (y.2 w))) (by norm_num))) ?_
  simp_rw [Finset.sum_add_distrib, mul_add]
  exact le_of_eq (by ring)

theorem convexBodySumFun_smul (c : â„) (x : E K) :
    convexBodySumFun (c â€¢ x) = |c| * convexBodySumFun x := by
  simp_rw [convexBodySumFun, Prod.smul_fst, Prod.smul_snd, Pi.smul_apply, smul_eq_mul,
    Complex.real_smul, norm_mul, Complex.norm_real, â† Finset.mul_sum, Real.norm_eq_abs]
  ring

theorem convexBodySumFun_eq_zero_iff (x : E K) :
    convexBodySumFun x = 0 â†” x = 0 := by
  refine âŸ¨fun h => ?_, fun h => ?_âŸ©
  Â· rw [add_eq_zero_iff' (Finset.sum_nonneg fun _ _ => norm_nonneg _) (mul_nonneg zero_le_two
      (Finset.sum_nonneg fun _ _ => norm_nonneg _)), Finset.mul_sum,
      Finset.sum_eq_zero_iff_of_nonneg (fun _ _ => mul_nonneg zero_le_two (norm_nonneg _)),
      Finset.sum_eq_zero_iff_of_nonneg (fun _ _ => norm_nonneg _)] at h
    ext : 2
    Â· exact norm_eq_zero.mp (h.1 _ (Finset.mem_univ _))
    Â· exact norm_eq_zero.1 <| eq_zero_of_ne_zero_of_mul_left_eq_zero two_ne_zero <| h.2 _ <|
        Finset.mem_univ _
  Â· simp only [convexBodySumFun, h, Prod.fst_zero, Pi.zero_apply, norm_zero, Finset.sum_const_zero,
      Prod.snd_zero, mul_zero, add_zero]

theorem norm_le_convexBodySumFun (x : E K) : â€–xâ€– â‰¤ convexBodySumFun x := by
  refine max_le ?_ ?_
  Â· refine (pi_norm_le_iff_of_nonneg (convexBodySumFun_nonneg x)).mpr (fun w => ?_)
    refine le_add_of_le_of_nonneg ?_ ?_
    Â· exact Finset.single_le_sum (fun z _ => norm_nonneg (x.1 z)) (Finset.mem_univ w)
    Â· exact mul_nonneg zero_le_two <| Finset.sum_nonneg (fun w _ => norm_nonneg (x.2 w))
  Â· refine (pi_norm_le_iff_of_nonneg (convexBodySumFun_nonneg x)).mpr (fun w => ?_)
    refine le_add_of_nonneg_of_le ?_ ?_
    Â· exact Finset.sum_nonneg (fun w _ => norm_nonneg (x.1 w))
    Â· rw [Finset.mul_sum]
      refine le_trans (by linarith [norm_nonneg (x.2 w)] : â€–x.2 wâ€– â‰¤ 2 * â€–x.2 wâ€–) ?_
      exact Finset.single_le_sum (fun z _ => mul_nonneg zero_le_two (norm_nonneg (x.2 z)))
        (Finset.mem_univ w)

variable (K)

theorem convexBodySumFun_continuous :
    Continuous (convexBodySumFun : (E K) â†’ â„) := by
  refine Continuous.add ?_ ?_
  Â· exact continuous_finset_sum Finset.univ
      (fun i _ => continuous_norm.comp' (continuous_apply i).fst')
  Â· refine Continuous.const_smul ?_ (2:â„)
    exact continuous_finset_sum Finset.univ
      (fun i _ => continuous_norm.comp' (continuous_apply i).snd')

/-- The convex body equal to the set of points `x : E` such that
  `âˆ‘ w real, â€–x wâ€– + 2 * âˆ‘ w complex, â€–x wâ€– â‰¤ B`. -/
abbrev convexBodySum : Set (E K)  := { x | convexBodySumFun x â‰¤ B }

theorem convexBodySum_volume_eq_zero_of_le_zero {B} (hB : B â‰¤ 0) :
    volume (convexBodySum K B) = 0 := by
  obtain hB | hB := lt_or_eq_of_le hB
  Â· suffices convexBodySum K B = âˆ… by rw [this, measure_empty]
    ext x
    refine âŸ¨fun hx => ?_, fun h => h.elimâŸ©
    rw [Set.mem_setOf] at hx
    linarith [convexBodySumFun_nonneg x]
  Â· suffices convexBodySum K B = { 0 } by rw [this, measure_singleton]
    ext
    rw [convexBodySum, Set.mem_setOf_eq, Set.mem_singleton_iff, hB, â† convexBodySumFun_eq_zero_iff]
    exact (convexBodySumFun_nonneg _).le_iff_eq

theorem convexBodySum_mem {x : K} :
    mixedEmbedding K x âˆˆ (convexBodySum K B) â†”
      âˆ‘ w : InfinitePlace K, (mult w) * w.val x â‰¤ B := by
  simp_rw [Set.mem_setOf_eq, mixedEmbedding, RingHom.prod_apply, convexBodySumFun, Pi.ringHom_apply,
    â† Complex.norm_real, embedding_of_isReal_apply, norm_embedding_eq, mult, Nat.cast_ite, ite_mul,
    Finset.sum_ite, Finset.filter_congr (fun _ _ => not_isReal_iff_isComplex), Finset.mul_sum,
    â† Finset.sum_subtype_eq_sum_filter, Finset.subtype_univ, Nat.cast_one, one_mul, Nat.cast_ofNat]
  rfl

theorem convexBodySum_symmetric {x : E K} (hx : x âˆˆ (convexBodySum K B)) :
    -x âˆˆ (convexBodySum K B) := by
  rw [Set.mem_setOf, convexBodySumFun_neg]
  exact hx

theorem convexBodySum_convex : Convex â„ (convexBodySum K B) := by
  refine Convex_subadditive_le (fun _ _ => convexBodySumFun_add_le _ _) (fun c x h => ?_) B
  convert le_of_eq (convexBodySumFun_smul c x)
  exact (abs_eq_self.mpr h).symm

theorem convexBodySum_isBounded : Bornology.IsBounded (convexBodySum K B) := by
  refine Metric.isBounded_iff.mpr âŸ¨B + B, fun x hx y hy => ?_âŸ©
  refine le_trans (norm_sub_le x y) (add_le_add ?_ ?_)
  exact le_trans (norm_le_convexBodySumFun x) hx
  exact le_trans (norm_le_convexBodySumFun y) hy

theorem convexBodySum_compact : IsCompact (convexBodySum K B) := by
  rw [Metric.isCompact_iff_isClosed_bounded]
  refine âŸ¨?_, convexBodySum_isBounded K BâŸ©
  convert IsClosed.preimage (convexBodySumFun_continuous K) (isClosed_Icc : IsClosed (Set.Icc 0 B))
  ext
  simp [convexBodySumFun_nonneg]

/-- The fudge factor that appears in the formula for the volume of `convexBodyLt`. -/
noncomputable abbrev convexBodySumFactor : â„â‰¥0âˆ :=
  (2:â„â‰¥0âˆ) ^ NrRealPlaces K * (NNReal.pi / 2) ^ NrComplexPlaces K / (finrank â„š K).factorial

theorem convexBodySumFactor_ne_zero : convexBodySumFactor K â‰  0 := by
  dsimp [convexBodySumFactor]
  refine mul_ne_zero (mul_ne_zero (pow_ne_zero _ two_ne_zero) ?_) ?_
  Â· refine ENNReal.pow_ne_zero ?_ _
    exact ne_of_gt <| div_pos_iff.mpr âŸ¨coe_ne_zero.mpr NNReal.pi_ne_zero, two_ne_topâŸ©
  Â· exact ENNReal.inv_ne_zero.mpr (nat_ne_top _)

theorem convexBodySumFactor_ne_top : convexBodySumFactor K â‰  âŠ¤ := by
  refine mul_ne_top (mul_ne_top (pow_ne_top two_ne_top) ?_) ?_
  Â· rw [show (2:â„â‰¥0âˆ) = (2:NNReal) by rfl, â† ENNReal.coe_div two_ne_zero]
    exact pow_ne_top coe_ne_top
  Â· exact inv_ne_top.mpr <| Nat.cast_ne_zero.mpr (Nat.factorial_ne_zero _)

open MeasureTheory MeasureTheory.Measure Real in
theorem convexBodySum_volume :
    volume (convexBodySum K B) = (convexBodySumFactor K) * (.ofReal B) ^ (finrank â„š K) := by
  obtain hB | hB := le_or_lt B 0
  Â· rw [convexBodySum_volume_eq_zero_of_le_zero K hB, ofReal_eq_zero.mpr hB, zero_pow, mul_zero]
    exact finrank_pos
  Â· suffices volume (convexBodySum K 1) = (convexBodySumFactor K) by
      rw [mul_comm]
      convert addHaar_smul volume B (convexBodySum K 1)
      Â· simp_rw [â† Set.preimage_smul_invâ‚€ (ne_of_gt hB), Set.preimage_setOf_eq, convexBodySumFun,
          Prod.smul_fst, Prod.smul_snd, Pi.smul_apply, Complex.real_smul, smul_eq_mul, norm_mul,
          Complex.ofReal_inv, norm_inv, norm_eq_abs B, Complex.norm_eq_abs B, Complex.abs_ofReal,
          abs_eq_self.mpr (le_of_lt hB), â† Finset.mul_sum, â† mul_assoc, mul_comm (2:â„), mul_assoc,
          â† mul_add, inv_mul_le_iff hB, mul_one]
      Â· rw [abs_pow, ofReal_pow (abs_nonneg _), abs_eq_self.mpr (le_of_lt hB),
          mixedEmbedding.finrank]
      Â· exact this.symm
    rw [MeasureTheory.measure_le_eq_lt _ ((convexBodySumFun_eq_zero_iff 0).mpr rfl)
      convexBodySumFun_neg convexBodySumFun_add_le
      (fun hx => (convexBodySumFun_eq_zero_iff _).mp hx)
      (fun r x => le_of_eq (convexBodySumFun_smul r x))]
    rw [measure_lt_one_eq_integral_div_gamma (g := fun x : (E K) => convexBodySumFun x)
      volume ((convexBodySumFun_eq_zero_iff 0).mpr rfl) convexBodySumFun_neg convexBodySumFun_add_le
      (fun hx => (convexBodySumFun_eq_zero_iff _).mp hx)
      (fun r x => le_of_eq (convexBodySumFun_smul r x)) zero_lt_one]
    simp_rw [mixedEmbedding.finrank, div_one, Gamma_nat_eq_factorial, ofReal_div_of_pos
      (Nat.cast_pos.mpr (Nat.factorial_pos _)), Real.rpow_one, ofReal_coe_nat]
    suffices âˆ« x : E K, exp (-convexBodySumFun x) =
        (2:â„) ^ NrRealPlaces K * (Ï€ / 2) ^ NrComplexPlaces K by
      rw [this, convexBodySumFactor, ofReal_mul (by positivity), ofReal_pow zero_le_two,
        ofReal_pow (by positivity), ofReal_div_of_pos zero_lt_two, ofReal_ofNat,
        â† NNReal.coe_real_pi, ofReal_coe_nnreal]
    calc
      _ = (âˆ« x : {w : InfinitePlace K // IsReal w} â†’ â„, âˆ w, exp (- â€–x wâ€–)) *
              (âˆ« x : {w : InfinitePlace K // IsComplex w} â†’ â„‚, âˆ w, exp (- 2 * â€–x wâ€–)) := by
        simp_rw [convexBodySumFun, neg_add, â† neg_mul, Finset.mul_sum, â† Finset.sum_neg_distrib,
          exp_add, exp_sum, â† integral_prod_mul, volume_eq_prod]
      _ = (âˆ« x : â„, exp (-|x|)) ^ NrRealPlaces K *
              (âˆ« x : â„‚, Real.exp (-2 * â€–xâ€–)) ^ NrComplexPlaces K := by
        rw [integral_fintype_prod_eq_pow _ (fun x => exp (- â€–xâ€–)), integral_fintype_prod_eq_pow _
          (fun x => exp (- 2 * â€–xâ€–))]
        simp_rw [norm_eq_abs]
      _ =  (2 * Gamma (1 / 1 + 1)) ^ NrRealPlaces K *
              (Ï€ * (2:â„) ^ (-(2:â„) / 1) * Gamma (2 / 1 + 1)) ^ NrComplexPlaces K := by
        rw [integral_comp_abs (f := fun x => exp (- x)), â† integral_exp_neg_rpow zero_lt_one,
          â† Complex.integral_exp_neg_mul_rpow le_rfl zero_lt_two]
        simp_rw [Real.rpow_one]
      _ = (2:â„) ^ NrRealPlaces K * (Ï€ / 2) ^ NrComplexPlaces K := by
        simp_rw [div_one, one_add_one_eq_two, Gamma_add_one two_ne_zero, Gamma_two, mul_one,
          mul_assoc, â† Real.rpow_add_one two_ne_zero, show (-2:â„) + 1 = -1 by norm_num,
          Real.rpow_neg_one]
        rfl

end convexBodySum

section minkowski

open MeasureTheory MeasureTheory.Measure Classical FiniteDimensional Zspan Real Submodule

open scoped ENNReal NNReal nonZeroDivisors

variable [NumberField K] (I : (Ideal (ğ“ K))â°)

/-- The bound that appears in **Minkowski Convex Body theorem**, see
`MeasureTheory.exists_ne_zero_mem_lattice_of_measure_mul_two_pow_lt_measure`. See
`NumberField.mixedEmbedding.volume_fundamentalDomain_idealLatticeBasis_eq` and
`NumberField.mixedEmbedding.volume_fundamentalDomain_latticeBasis` for the computation of
`volume (fundamentalDomain (idealLatticeBasis K))`. -/
noncomputable def minkowskiBound : â„â‰¥0âˆ :=
  volume (fundamentalDomain (idealLatticeBasis K I)) * (2 : â„â‰¥0âˆ) ^ (finrank â„ (E K))

theorem volume_fundamentalDomain_idealLatticeBasis_eq :
    volume (fundamentalDomain (idealLatticeBasis K I)) =
      Ideal.absNorm (I : Ideal (ğ“ K)) *
        volume (fundamentalDomain (latticeBasis K)) := by
  let e : (Module.Free.ChooseBasisIndex â„¤ I) â‰ƒ (Module.Free.ChooseBasisIndex â„¤ (ğ“ K)) := by
    refine Fintype.equivOfCardEq ?_
    rw [â† finrank_eq_card_chooseBasisIndex, â† finrank_eq_card_chooseBasisIndex, ideal_rank_eq]
  rw [â† fundamentalDomain_reindex (idealLatticeBasis K I) e,
    measure_fundamentalDomain ((idealLatticeBasis K I).reindex e), show
    (Basis.reindex (idealLatticeBasis K I) e) = (mixedEmbedding K) âˆ˜ (idealBasis K I) âˆ˜
    e.symm by ext1; simp only [Basis.coe_reindex, Function.comp_apply, idealLatticeBasis_apply],
    det_idealBasis_eq_norm, ENNReal.ofReal_coe_nat]

theorem minkowskiBound_lt_top : minkowskiBound K I < âŠ¤ := by
  refine ENNReal.mul_lt_top ?_ ?_
  Â· exact ne_of_lt (fundamentalDomain_isBounded _).measure_lt_top
  Â· exact ne_of_lt (ENNReal.pow_lt_top (lt_top_iff_ne_top.mpr ENNReal.two_ne_top) _)

theorem minkowskiBound_pos : 0 < minkowskiBound K I := by
  refine zero_lt_iff.mpr (mul_ne_zero ?_ ?_)
  Â· exact Zspan.measure_fundamentalDomain_ne_zero _
  Â· exact ENNReal.pow_ne_zero two_ne_zero _

variable {f : InfinitePlace K â†’ â„â‰¥0} (I : (Ideal (ğ“ K))â°)

/-- Let `I` be an integral ideal of `K`. Assume that `f : InfinitePlace K â†’ â„â‰¥0` is such that
`minkowskiBound K I < volume (convexBodyLT K f)` where `convexBodyLT K f` is the set of
points `x` such that `â€–x wâ€– < f w` for all infinite places `w` (see `convexBodyLT_volume` for
the computation of this volume), then there exists a nonzero algebraic integer `a` in `I` such
that `w a < f w` for all infinite places `w`. -/
theorem exists_ne_zero_mem_ideal_lt (h : minkowskiBound K I < volume (convexBodyLT K f)) :
    âˆƒ a âˆˆ (I : Ideal (ğ“ K)), a â‰  0 âˆ§ âˆ€ w : InfinitePlace K, w a < f w := by
  have h_fund := Zspan.isAddFundamentalDomain (idealLatticeBasis K I) volume
  have : Countable (span â„¤ (Set.range (idealLatticeBasis K I))).toAddSubgroup := by
    change Countable (span â„¤ (Set.range (idealLatticeBasis K I)) : Set (E K))
    infer_instance
  obtain âŸ¨âŸ¨x, hxâŸ©, h_nzr, h_memâŸ© := exists_ne_zero_mem_lattice_of_measure_mul_two_pow_lt_measure
    h_fund (convexBodyLT_symmetric K f) (convexBodyLT_convex K f) h
  rw [mem_toAddSubgroup, mem_span_idealLatticeBasis] at hx
  obtain âŸ¨_, âŸ¨a, ha, rflâŸ©, rflâŸ© := hx
  refine âŸ¨a, ha, ?_, (convexBodyLT_mem K f).mp h_memâŸ©
  rw [ne_eq, AddSubgroup.mk_eq_zero_iff, map_eq_zero, â† ne_eq] at h_nzr
  exact Subtype.ne_of_val_ne h_nzr

/-- A version of `exists_ne_zero_mem_ideal_lt` for the ring of integers of `K`. -/
theorem exists_ne_zero_mem_ringOfIntegers_lt (h : minkowskiBound K 1 < volume (convexBodyLT K f)) :
    âˆƒ (a : ğ“ K), a â‰  0 âˆ§ âˆ€ w : InfinitePlace K, w a < f w := by
  obtain âŸ¨a, _, _âŸ© := exists_ne_zero_mem_ideal_lt K 1 h
  use a

theorem exists_ne_zero_mem_ideal_of_norm_le {B : â„}
    (h : (minkowskiBound K I) â‰¤ volume (convexBodySum K B)) :
    âˆƒ a âˆˆ (I : Ideal (ğ“ K)), a â‰  0 âˆ§
      |Algebra.norm â„š (a:K)| â‰¤ (B / (finrank â„š K)) ^ (finrank â„š K) := by
  have hB : 0 â‰¤ B := by
    contrapose! h
    rw [convexBodySum_volume_eq_zero_of_le_zero K (le_of_lt h)]
    exact minkowskiBound_pos K I
  -- Some inequalities that will be useful later on
  have h1 : 0 < (finrank â„š K : â„)â»Â¹ := inv_pos.mpr (Nat.cast_pos.mpr finrank_pos)
  have h2 : 0 â‰¤ B / (finrank â„š K) := div_nonneg hB (Nat.cast_nonneg _)
  have h_fund := Zspan.isAddFundamentalDomain (idealLatticeBasis K I) volume
  have : Countable (span â„¤ (Set.range (idealLatticeBasis K I))).toAddSubgroup := by
    change Countable (span â„¤ (Set.range (idealLatticeBasis K I)): Set (E K))
    infer_instance
  have : DiscreteTopology (span â„¤ (Set.range (idealLatticeBasis K I))).toAddSubgroup := by
    change DiscreteTopology (span â„¤ (Set.range (idealLatticeBasis K I)): Set (E K))
    infer_instance
  obtain âŸ¨âŸ¨x, hxâŸ©, h_nzr, h_memâŸ© := exists_ne_zero_mem_lattice_of_measure_mul_two_pow_le_measure
      h_fund (fun _ â†¦ convexBodySum_symmetric K B) (convexBodySum_convex K B)
      (convexBodySum_compact K B) h
  rw [mem_toAddSubgroup, mem_span_idealLatticeBasis] at hx
  obtain âŸ¨_, âŸ¨a, ha, rflâŸ©, rflâŸ© := hx
  refine âŸ¨a, ha, ?_, ?_âŸ©
  Â· rw [ne_eq, AddSubgroup.mk_eq_zero_iff, map_eq_zero, â† ne_eq] at h_nzr
    exact Subtype.ne_of_val_ne h_nzr
  Â· rw [â† rpow_nat_cast, â† rpow_le_rpow_iff (by simp only [Rat.cast_abs, abs_nonneg])
      (rpow_nonneg h2 _) h1, â† rpow_mul h2,  mul_inv_cancel (Nat.cast_ne_zero.mpr
      (ne_of_gt finrank_pos)), rpow_one, le_div_iff' (Nat.cast_pos.mpr finrank_pos)]
    refine le_trans ?_ ((convexBodySum_mem K B).mp h_mem)
    rw [â† le_div_iff' (Nat.cast_pos.mpr finrank_pos), â† sum_mult_eq, Nat.cast_sum]
    refine le_trans ?_ (geom_mean_le_arith_mean Finset.univ _ _ (fun _ _ => Nat.cast_nonneg _)
      ?_ (fun _ _ => AbsoluteValue.nonneg _ _))
    Â· simp_rw [â† prod_eq_abs_norm, rpow_nat_cast]
      exact le_of_eq rfl
    Â· rw [â† Nat.cast_sum, sum_mult_eq, Nat.cast_pos]
      exact finrank_pos

theorem exists_ne_zero_mem_ringOfIntegers_of_norm_le {B : â„}
    (h : (minkowskiBound K 1) â‰¤ volume (convexBodySum K B)) :
    âˆƒ (a : ğ“ K), a â‰  0 âˆ§ |Algebra.norm â„š (a:K)| â‰¤ (B / (finrank â„š K)) ^ (finrank â„š K) := by
  obtain âŸ¨a, _, _âŸ© := exists_ne_zero_mem_ideal_of_norm_le K 1 h
  use a

end minkowski

end NumberField.mixedEmbedding
