/-
Copyright (c) 2022 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.NumberTheory.NumberField.Embeddings

#align_import number_theory.number_field.canonical_embedding from "leanprover-community/mathlib"@"60da01b41bbe4206f05d34fd70c8dd7498717a30"

/-!
# Canonical embedding of a number field

The canonical embedding of a number field `K` of signature `(r‚ÇÅ, r‚ÇÇ)` is the ring homomorphism
`K ‚Üí+* ‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ` that sends `x ‚àà K` to `(œÜ_‚ÇÅ(x),...,œÜ_r‚ÇÅ(x)) √ó (œà_‚ÇÅ(x),..., œà_r‚ÇÇ(x))` where
`œÜ_‚ÇÅ,...,œÜ_r‚ÇÅ` are its real embeddings and `œà_‚ÇÅ,..., œà_r‚ÇÇ` are its complex embeddings (up to
complex conjugation).

## Main definitions and results

* `NumberField.canonicalEmbedding.integerLattice.inter_ball_finite`: the intersection of the
image of the ring of integers by the canonical embedding and any ball centered at `0` of finite
radius is finite.

## Tags

number field, infinite places
-/


noncomputable section

open Function FiniteDimensional Finset Fintype NumberField NumberField.InfinitePlace Metric Module

open scoped Classical NumberField

variable (K : Type*) [Field K]

namespace NumberField.canonicalEmbedding

-- The ambient space `‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ` with `(r‚ÇÅ, r‚ÇÇ)` the signature of `K`.
set_option quotPrecheck false in -- Porting note: Added.
scoped[CanonicalEmbedding]
  notation "E" =>
    ({ w : InfinitePlace K // IsReal w } ‚Üí ‚Ñù) √ó ({ w : InfinitePlace K // IsComplex w } ‚Üí ‚ÑÇ)

open CanonicalEmbedding

theorem space_rank [NumberField K] : finrank ‚Ñù E = finrank ‚Ñö K := by
  haveI : Module.Free ‚Ñù ‚ÑÇ := inferInstance
  rw [finrank_prod, finrank_pi, finrank_pi_fintype, Complex.finrank_real_complex, Finset.sum_const,
    Finset.card_univ, ‚Üê card_real_embeddings, Algebra.id.smul_eq_mul, mul_comm,
    ‚Üê card_complex_embeddings, ‚Üê NumberField.Embeddings.card K ‚ÑÇ, Fintype.card_subtype_compl,
    Nat.add_sub_of_le (Fintype.card_subtype_le _)]
#align number_field.canonical_embedding.space_rank NumberField.canonicalEmbedding.space_rank

theorem nontrivial_space [NumberField K] : Nontrivial E := by
  have : Nonempty <| InfinitePlace K := inferInstance
  rcases this with ‚ü®w‚ü©
  obtain hw | hw := w.isReal_or_isComplex
  ¬∑ haveI : Nonempty { w : InfinitePlace K // IsReal w } := ‚ü®‚ü®w, hw‚ü©‚ü©
    exact nontrivial_prod_left
  ¬∑ haveI : Nonempty { w : InfinitePlace K // IsComplex w } := ‚ü®‚ü®w, hw‚ü©‚ü©
    exact nontrivial_prod_right
#align number_field.canonical_embedding.non_trivial_space NumberField.canonicalEmbedding.nontrivial_space

/-- The canonical embedding of a number field `K` of signature `(r‚ÇÅ, r‚ÇÇ)` into `‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ`. -/
def _root_.NumberField.canonicalEmbedding : K ‚Üí+* E :=
  RingHom.prod (Pi.ringHom fun w => embedding_of_isReal w.prop)
    (Pi.ringHom fun w => w.val.embedding)
#align number_field.canonical_embedding NumberField.canonicalEmbedding

theorem _root_.NumberField.canonicalEmbedding_injective [NumberField K] :
    Injective (NumberField.canonicalEmbedding K) :=
  @RingHom.injective _ _ _ _ (nontrivial_space K) _
#align number_field.canonical_embedding_injective NumberField.canonicalEmbedding_injective

open NumberField

variable {K}

@[simp]
theorem apply_at_real_infinitePlace (w : { w : InfinitePlace K // IsReal w }) (x : K) :
    (NumberField.canonicalEmbedding K x).1 w = embedding_of_isReal w.prop x := by
  simp only [canonicalEmbedding, RingHom.prod_apply, Pi.ringHom_apply]
#align number_field.canonical_embedding.apply_at_real_infinite_place NumberField.canonicalEmbedding.apply_at_real_infinitePlace

@[simp]
theorem apply_at_complex_infinitePlace (w : { w : InfinitePlace K // IsComplex w }) (x : K) :
    (NumberField.canonicalEmbedding K x).2 w = embedding w.val x := by
  simp only [canonicalEmbedding, RingHom.prod_apply, Pi.ringHom_apply]
#align number_field.canonical_embedding.apply_at_complex_infinite_place NumberField.canonicalEmbedding.apply_at_complex_infinitePlace

theorem nnnorm_eq [NumberField K] (x : K) :
    ‚ÄñcanonicalEmbedding K x‚Äñ‚Çä =
      Finset.univ.sup fun w : InfinitePlace K => ‚ü®w x, map_nonneg w x‚ü© := by
  rw [Prod.nnnorm_def', Pi.nnnorm_def, Pi.nnnorm_def]
  rw [(_ : Finset.univ =
        {w : InfinitePlace K | IsReal w}.toFinset ‚à™ {w : InfinitePlace K | IsComplex w}.toFinset)]
  ¬∑ rw [Finset.sup_union, sup_eq_max]
    refine' congr_arg‚ÇÇ _ _ _
    ¬∑ convert
        (Finset.univ.sup_map (Function.Embedding.subtype fun w : InfinitePlace K => IsReal w)
          fun w => (‚ü®w x, map_nonneg w x‚ü© : NNReal)).symm using 2
      ext w
      dsimp
      rw [apply_at_real_infinitePlace, ‚Üê Complex.abs_ofReal, embedding_of_isReal_apply,
        ‚Üê Complex.norm_eq_abs, norm_embedding_eq]
    ¬∑ convert
        (Finset.univ.sup_map (Function.Embedding.subtype fun w : InfinitePlace K => IsComplex w)
          fun w => (‚ü®w x, map_nonneg w x‚ü© : NNReal)).symm using 2
      ext w
      dsimp
      rw [apply_at_complex_infinitePlace, ‚Üê Complex.norm_eq_abs, norm_embedding_eq]
  ¬∑ ext w
    simp_rw [Finset.mem_univ, Finset.mem_union, Set.mem_toFinset, Set.mem_setOf_eq,
      w.isReal_or_isComplex]
#align number_field.canonical_embedding.nnnorm_eq NumberField.canonicalEmbedding.nnnorm_eq

theorem norm_le_iff [NumberField K] (x : K) (r : ‚Ñù) :
    ‚ÄñcanonicalEmbedding K x‚Äñ ‚â§ r ‚Üî ‚àÄ w : InfinitePlace K, w x ‚â§ r := by
  obtain hr | hr := lt_or_le r 0
  ¬∑ have : Nonempty <| InfinitePlace K := inferInstance
    rcases this with ‚ü®w‚ü©
    exact iff_of_false
      (hr.trans_le <| norm_nonneg _).not_le fun h => hr.not_le <| (map_nonneg w _).trans <| h _
  ¬∑ lift r to NNReal using hr
    simp_rw [‚Üê coe_nnnorm, nnnorm_eq, NNReal.coe_le_coe, Finset.sup_le_iff, Finset.mem_univ,
      forall_true_left, ‚Üê NNReal.coe_le_coe, NNReal.toReal]
#align number_field.canonical_embedding.norm_le_iff NumberField.canonicalEmbedding.norm_le_iff

variable (K)

/-- The image of `ùìû K` as a subring of `‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ`. -/
def integerLattice : Subring E :=
  (RingHom.range (algebraMap (ùìû K) K)).map (canonicalEmbedding K)
#align number_field.canonical_embedding.integer_lattice NumberField.canonicalEmbedding.integerLattice

-- Porting note: See https://github.com/leanprover-community/mathlib4/issues/5028
set_option maxHeartbeats 400000 in
set_option synthInstance.maxHeartbeats 50000 in
/-- The linear equiv between `ùìû K` and the integer lattice. -/
def equivIntegerLattice [NumberField K] : ùìû K ‚âÉ‚Çó[‚Ñ§] integerLattice K :=
  LinearEquiv.ofBijective
    { toFun := fun x => (by
          refine ‚ü®canonicalEmbedding K (algebraMap (ùìû K) K x), ‚ü®algebraMap (ùìû K) K x, ‚ü®?_, rfl‚ü©‚ü©‚ü©
          simp only [Subsemiring.coe_carrier_toSubmonoid, Subring.coe_toSubsemiring,
            RingHom.coe_range, Set.mem_range, exists_apply_eq_apply] )
      map_add' := fun x y => (by
          apply Subtype.eq
          simp [map_add] )
      map_smul' := fun c x => (by
          simp only [RingHom.id_apply, zsmul_eq_mul, RingHom.map_mul, map_intCast]
          rfl ) }
   (by
    refine ‚ü®fun _ _ h => ?_, fun ‚ü®_, ‚ü®_, ‚ü®‚ü®a, rfl‚ü©, rfl‚ü©‚ü©‚ü© => ‚ü®a, rfl‚ü©‚ü©
    dsimp only at h
    rw [LinearMap.coe_mk, Subtype.mk_eq_mk] at h
    exact IsFractionRing.injective (ùìû K) K (canonicalEmbedding_injective K h))
#align number_field.canonical_embedding.equiv_integer_lattice NumberField.canonicalEmbedding.equivIntegerLattice

theorem integerLattice.inter_ball_finite [NumberField K] (r : ‚Ñù) :
    ((integerLattice K : Set E) ‚à© closedBall 0 r).Finite := by
  obtain hr | _ := lt_or_le r 0
  ¬∑ simp [closedBall_eq_empty.2 hr]
  have heq : ‚àÄ x, canonicalEmbedding K x ‚àà closedBall (0 : E) r ‚Üî ‚àÄ œÜ : K ‚Üí+* ‚ÑÇ, ‚ÄñœÜ x‚Äñ ‚â§ r := by
    simp_rw [‚Üê place_apply, mem_closedBall_zero_iff, norm_le_iff, le_iff_le, place_apply,
      implies_true]
  convert (Embeddings.finite_of_norm_le K ‚ÑÇ r).image (canonicalEmbedding K)
  ext; constructor
  ¬∑ rintro ‚ü®‚ü®_, ‚ü®x, rfl‚ü©, rfl‚ü©, hx2‚ü©
    exact ‚ü®x, ‚ü®SetLike.coe_mem x, (heq x).mp hx2‚ü©, rfl‚ü©
  ¬∑ rintro ‚ü®x, ‚ü®hx1, hx2‚ü©, rfl‚ü©
    exact ‚ü®‚ü®x, ‚ü®‚ü®x, hx1‚ü©, rfl‚ü©, rfl‚ü©, (heq x).mpr hx2‚ü©
#align number_field.canonical_embedding.integer_lattice.inter_ball_finite NumberField.canonicalEmbedding.integerLattice.inter_ball_finite

instance [NumberField K] : Countable (integerLattice K) := by
  have : (‚ãÉ n : ‚Ñï, (integerLattice K : Set E) ‚à© closedBall 0 n).Countable :=
    Set.countable_iUnion fun n => (integerLattice.inter_ball_finite K n).countable
  refine' (this.mono _).to_subtype
  rintro _ ‚ü®x, hx, rfl‚ü©
  rw [Set.mem_iUnion]
  exact ‚ü®‚åà‚ÄñcanonicalEmbedding K x‚Äñ‚åâ‚Çä, ‚ü®x, hx, rfl‚ü©, mem_closedBall_zero_iff.2 (Nat.le_ceil _)‚ü©

end NumberField.canonicalEmbedding
