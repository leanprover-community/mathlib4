/-
Copyright (c) 2022 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.Algebra.Module.Zlattice
import Mathlib.MeasureTheory.Group.GeometryOfNumbers
import Mathlib.MeasureTheory.Measure.Haar.NormedSpace
import Mathlib.NumberTheory.NumberField.Embeddings
import Mathlib.RingTheory.Discriminant

#align_import number_theory.number_field.canonical_embedding from "leanprover-community/mathlib"@"60da01b41bbe4206f05d34fd70c8dd7498717a30"

/-!
# Canonical embedding of a number field

The canonical embedding of a number field `K` of degree `n` is the ring homomorphism
`K â†’+* â„‚^n` that sends `x âˆˆ K` to `(Ï†_â‚(x),...,Ï†_n(x))` where the `Ï†_i`'s are the complex
embeddings of `K`. Note that we do not choose an ordering of the embeddings, but instead map `K`
into the type `(K â†’+* â„‚) â†’ â„‚` of `â„‚`-vectors indexed by the complex embeddings.

## Main definitions and results

* `canonicalEmbedding`: the ring homorphism `K â†’+* ((K â†’+* â„‚) â†’ â„‚)` defined by sending `x : K` to
the vector `(Ï† x)` indexed by `Ï† : K â†’+* â„‚`.

* `canonicalEmbedding.integerLattice.inter_ball_finite`: the intersection of the
image of the ring of integers by the canonical embedding and any ball centered at `0` of finite
radius is finite.

* `NumberField.mixedEmbedding`: the ring homomorphism `K â†’+* â„^râ‚ Ã— â„‚^râ‚‚` that sends `x âˆˆ K` to
`(Ï†_â‚(x),...,Ï†_râ‚(x)) Ã— (Ïˆ_â‚(x),..., Ïˆ_râ‚‚(x)) ` where `(râ‚, râ‚‚)` is the signature of `K`,
`Ï†_â‚,...,Ï†_râ‚` are its real embeddings and `Ïˆ_â‚,..., Ïˆ_râ‚‚` are its complex embeddings (up to
complex conjugation).

* `exists_ne_zero_mem_ringOfIntegers_lt`: let `f : InfinitePlace K â†’ â„â‰¥0`, if the product
`âˆ w, f w` is large enough, proves that there exists a nonzero algebraic integer `a` such
that `w a < f w` for all infinite places `w`.

## Tags

number field, infinite places
-/

variable (K : Type*) [Field K]

namespace NumberField.canonicalEmbedding

open NumberField

/-- The canonical embedding of a number field `K` of degree `n` into `â„‚^n`. -/
def _root_.NumberField.canonicalEmbedding : K â†’+* ((K â†’+* â„‚) â†’ â„‚) := Pi.ringHom fun Ï† => Ï†

theorem _root_.NumberField.canonicalEmbedding_injective [NumberField K] :
    Function.Injective (NumberField.canonicalEmbedding K) := RingHom.injective _

variable {K}

@[simp]
theorem apply_at (Ï† : K â†’+* â„‚) (x : K) : (NumberField.canonicalEmbedding K x) Ï† = Ï† x := rfl

open scoped ComplexConjugate

/-- The image of `canonicalEmbedding` lives in the `â„`-submodule of the `x âˆˆ ((K â†’+* â„‚) â†’ â„‚)` such
that `conj x_Ï† = x_(conj Ï†)` for all `Ï† : K â†’+* â„‚`. -/
theorem conj_apply {x : ((K â†’+* â„‚) â†’ â„‚)} (Ï† : K â†’+* â„‚)
    (hx : x âˆˆ Submodule.span â„ (Set.range (canonicalEmbedding K))) :
    conj (x Ï†) = x (ComplexEmbedding.conjugate Ï†) := by
  refine Submodule.span_induction hx ?_ ?_ (fun _ _ hx hy => ?_) (fun a _ hx => ?_)
  Â· rintro _ âŸ¨x, rflâŸ©
    rw [apply_at, apply_at, ComplexEmbedding.conjugate_coe_eq]
  Â· rw [Pi.zero_apply, Pi.zero_apply, map_zero]
  Â· rw [Pi.add_apply, Pi.add_apply, map_add, hx, hy]
  Â· rw [Pi.smul_apply, Complex.real_smul, map_mul, Complex.conj_ofReal]
    exact congrArg ((a : â„‚) * Â·) hx

theorem nnnorm_eq [NumberField K] (x : K) :
    â€–canonicalEmbedding K xâ€–â‚Š = Finset.univ.sup (fun Ï† : K â†’+* â„‚ => â€–Ï† xâ€–â‚Š) := by
  simp_rw [Pi.nnnorm_def, apply_at]

theorem norm_le_iff [NumberField K] (x : K) (r : â„) :
    â€–canonicalEmbedding K xâ€– â‰¤ r â†” âˆ€ Ï† : K â†’+* â„‚, â€–Ï† xâ€– â‰¤ r := by
  obtain hr | hr := lt_or_le r 0
  Â· obtain âŸ¨Ï†âŸ© := (inferInstance : Nonempty (K â†’+* â„‚))
    refine iff_of_false ?_ ?_
    exact (hr.trans_le (norm_nonneg _)).not_le
    exact fun h => hr.not_le (le_trans (norm_nonneg _) (h Ï†))
  Â· lift r to NNReal using hr
    simp_rw [â† coe_nnnorm, nnnorm_eq, NNReal.coe_le_coe, Finset.sup_le_iff, Finset.mem_univ,
      forall_true_left]

variable (K)

/-- The image of `ğ“ K` as a subring of `â„‚^n`. -/
def integerLattice : Subring ((K â†’+* â„‚) â†’ â„‚) :=
  (RingHom.range (algebraMap (ğ“ K) K)).map (canonicalEmbedding K)

theorem integerLattice.inter_ball_finite [NumberField K] (r : â„) :
    ((integerLattice K : Set ((K â†’+* â„‚) â†’ â„‚)) âˆ© Metric.closedBall 0 r).Finite := by
  obtain hr | _ := lt_or_le r 0
  Â· simp [Metric.closedBall_eq_empty.2 hr]
  Â· have heq : âˆ€ x, canonicalEmbedding K x âˆˆ Metric.closedBall 0 r â†”
        âˆ€ Ï† : K â†’+* â„‚, â€–Ï† xâ€– â‰¤ r := by
      intro x; rw [â† norm_le_iff, mem_closedBall_zero_iff]
    convert (Embeddings.finite_of_norm_le K â„‚ r).image (canonicalEmbedding K)
    ext; constructor
    Â· rintro âŸ¨âŸ¨_, âŸ¨x, rflâŸ©, rflâŸ©, hxâŸ©
      exact âŸ¨â†‘x, âŸ¨SetLike.coe_mem x, fun Ï† => (heq x).mp hx Ï†âŸ©, rflâŸ©
    Â· rintro âŸ¨x, âŸ¨hx1, hx2âŸ©, rflâŸ©
      exact âŸ¨âŸ¨x, âŸ¨âŸ¨x, hx1âŸ©, rflâŸ©, rflâŸ©, (heq x).mpr hx2âŸ©

open Module Fintype FiniteDimensional

/-- A `â„‚`-basis of `â„‚^n` that is also a `â„¤`-basis of the `integerLattice`. -/
noncomputable def latticeBasis [NumberField K] :
    Basis (Free.ChooseBasisIndex â„¤ (ğ“ K)) â„‚ ((K â†’+* â„‚) â†’ â„‚) := by
  classical
  -- Let `B` be the canonical basis of `(K â†’+* â„‚) â†’ â„‚`. We prove that the determinant of
  -- the image by `canonicalEmbedding` of the integral basis of `K` is nonzero. This
  -- will imply the result.
    let B := Pi.basisFun â„‚ (K â†’+* â„‚)
    let e : (K â†’+* â„‚) â‰ƒ Free.ChooseBasisIndex â„¤ (ğ“ K) :=
      equivOfCardEq ((Embeddings.card K â„‚).trans (finrank_eq_card_basis (integralBasis K)))
    let M := B.toMatrix (fun i => canonicalEmbedding K (integralBasis K (e i)))
    suffices M.det â‰  0 by
      rw [â† isUnit_iff_ne_zero, â† Basis.det_apply, â† is_basis_iff_det] at this
      refine basisOfLinearIndependentOfCardEqFinrank
        ((linearIndependent_equiv e.symm).mpr this.1) ?_
      rw [â† finrank_eq_card_chooseBasisIndex, RingOfIntegers.rank, finrank_fintype_fun_eq_card,
        Embeddings.card]
  -- In order to prove that the determinant is nonzero, we show that it is equal to the
  -- square of the discriminant of the integral basis and thus it is not zero
    let N := Algebra.embeddingsMatrixReindex â„š â„‚ (fun i => integralBasis K (e i))
      RingHom.equivRatAlgHom
    rw [show M = N.transpose by { ext:2; rfl }]
    rw [Matrix.det_transpose, â† @pow_ne_zero_iff â„‚ _ _ _ 2 (by norm_num)]
    convert (map_ne_zero_iff _ (algebraMap â„š â„‚).injective).mpr
      (Algebra.discr_not_zero_of_basis â„š (integralBasis K))
    rw [â† Algebra.discr_reindex â„š (integralBasis K) e.symm]
    exact (Algebra.discr_eq_det_embeddingsMatrixReindex_pow_two â„š â„‚
      (fun i => integralBasis K (e i)) RingHom.equivRatAlgHom).symm

@[simp]
theorem latticeBasis_apply [NumberField K] (i : Free.ChooseBasisIndex â„¤ (ğ“ K)) :
    latticeBasis K i = (canonicalEmbedding K) (integralBasis K i) := by
  simp only [latticeBasis, integralBasis_apply, coe_basisOfLinearIndependentOfCardEqFinrank,
    Function.comp_apply, Equiv.apply_symm_apply]

theorem mem_span_latticeBasis [NumberField K] (x : (K â†’+* â„‚) â†’ â„‚) :
    x âˆˆ Submodule.span â„¤ (Set.range (latticeBasis K)) â†” x âˆˆ canonicalEmbedding K '' (ğ“ K) := by
  rw [show Set.range (latticeBasis K) =
      (canonicalEmbedding K).toIntAlgHom.toLinearMap '' (Set.range (integralBasis K)) by
    rw [â† Set.range_comp]; exact congrArg Set.range (funext (fun i => latticeBasis_apply K i))]
  rw [â† Submodule.map_span, â† SetLike.mem_coe, Submodule.map_coe]
  rw [show (Submodule.span â„¤ (Set.range (integralBasis K)) : Set K) = ğ“ K by
    ext; exact mem_span_integralBasis K]
  rfl

end NumberField.canonicalEmbedding

namespace NumberField.mixedEmbedding

open NumberField NumberField.InfinitePlace NumberField.ComplexEmbedding

/-- The ambient space `â„^râ‚ Ã— â„‚^râ‚‚` with `(râ‚, râ‚‚)` the signature of `K`. -/
local notation "E" K =>
  ({ w : InfinitePlace K // IsReal w } â†’ â„) Ã— ({ w : InfinitePlace K // IsComplex w } â†’ â„‚)

/-- The canonical embedding of a number field `K` of signature `(râ‚, râ‚‚)` into `â„^râ‚ Ã— â„‚^râ‚‚`. -/
noncomputable def _root_.NumberField.mixedEmbedding : K â†’+* (E K) :=
  RingHom.prod (Pi.ringHom fun w => embedding_of_isReal w.prop)
    (Pi.ringHom fun w => w.val.embedding)

instance [NumberField K] :  Nontrivial (E K) := by
  obtain âŸ¨wâŸ© := (inferInstance : Nonempty (InfinitePlace K))
  obtain hw | hw := w.isReal_or_isComplex
  Â· have : Nonempty {w : InfinitePlace K // IsReal w} := âŸ¨âŸ¨w, hwâŸ©âŸ©
    exact nontrivial_prod_left
  Â· have : Nonempty {w : InfinitePlace K // IsComplex w} := âŸ¨âŸ¨w, hwâŸ©âŸ©
    exact nontrivial_prod_right

theorem _root_.NumberField.mixedEmbedding_injective [NumberField K] :
    Function.Injective (NumberField.mixedEmbedding K) := by
  exact RingHom.injective _

section comm_map

/-- The linear map that makes `canonicalEmbedding` and `mixedEmbedding` commute, see
`comm_map_canonical_eq_mixed`. -/
noncomputable def comm_map : ((K â†’+* â„‚) â†’ â„‚) â†’â‚—[â„] (E K) :=
{ toFun := fun x => âŸ¨fun w => (x w.val.embedding).re, fun w => x w.val.embeddingâŸ©
  map_add' := by
    simp only [Pi.add_apply, Complex.add_re, Prod.mk_add_mk, Prod.mk.injEq]
    exact fun _ _ => âŸ¨rfl, rflâŸ©
  map_smul' := by
    simp only [Pi.smul_apply, Complex.real_smul, Complex.mul_re, Complex.ofReal_re,
      Complex.ofReal_im, zero_mul, sub_zero, RingHom.id_apply, Prod.smul_mk, Prod.mk.injEq]
    exact fun _ _ => âŸ¨rfl, rflâŸ© }

theorem comm_map_apply_of_isReal (x : (K â†’+* â„‚) â†’ â„‚) {w : InfinitePlace K} (hw : IsReal w) :
  (comm_map K x).1 âŸ¨w, hwâŸ© = (x w.embedding).re := rfl

theorem comm_map_apply_of_isComplex (x : (K â†’+* â„‚) â†’ â„‚) {w : InfinitePlace K} (hw : IsComplex w) :
  (comm_map K x).2 âŸ¨w, hwâŸ© = x w.embedding := rfl

@[simp]
theorem comm_map_canonical_eq_mixed (x : K) :
    comm_map K (canonicalEmbedding K x) = mixedEmbedding K x := by
  simp only [canonicalEmbedding, comm_map, LinearMap.coe_mk, AddHom.coe_mk, Pi.ringHom_apply,
    mixedEmbedding, RingHom.prod_apply, Prod.mk.injEq]
  exact âŸ¨rfl, rflâŸ©

/-- This is a technical result to ensure that the image of the `â„‚`-basis of `â„‚^n` defined in
`canonicalEmbedding.latticeBasis` is a `â„`-basis of `â„^râ‚ Ã— â„‚^râ‚‚`,
see `mixedEmbedding.latticeBasis`. -/
theorem disjoint_span_comm_map_ker [NumberField K]:
    Disjoint (Submodule.span â„ (Set.range (canonicalEmbedding.latticeBasis K)))
      (LinearMap.ker (comm_map K)) := by
  refine LinearMap.disjoint_ker.mpr (fun x h_mem h_zero => ?_)
  replace h_mem : x âˆˆ Submodule.span â„ (Set.range (canonicalEmbedding K)) := by
    refine (Submodule.span_mono ?_) h_mem
    rintro _ âŸ¨i, rflâŸ©
    exact âŸ¨integralBasis K i, (canonicalEmbedding.latticeBasis_apply K i).symmâŸ©
  ext1 Ï†
  rw [Pi.zero_apply]
  by_cases hÏ† : IsReal Ï†
  Â· rw [show x Ï† = (x Ï†).re by
      rw [eq_comm, â† Complex.conj_eq_iff_re, canonicalEmbedding.conj_apply _ h_mem,
        ComplexEmbedding.isReal_iff.mp hÏ†], â† Complex.ofReal_zero]
    congr
    rw [â† embedding_mk_eq_of_isReal hÏ†, â† comm_map_apply_of_isReal K x âŸ¨Ï†, hÏ†, rflâŸ©]
    exact congrFun (congrArg (fun x => x.1) h_zero) âŸ¨InfinitePlace.mk Ï†, _âŸ©
  Â· have := congrFun (congrArg (fun x => x.2) h_zero) âŸ¨InfinitePlace.mk Ï†, âŸ¨Ï†, hÏ†, rflâŸ©âŸ©
    cases embedding_mk_eq Ï† with
    | inl h => rwa [â† h, â† comm_map_apply_of_isComplex K x âŸ¨Ï†, hÏ†, rflâŸ©]
    | inr h =>
        apply RingHom.injective (starRingEnd â„‚)
        rwa [canonicalEmbedding.conj_apply _ h_mem, â† h, map_zero,
          â† comm_map_apply_of_isComplex K x âŸ¨Ï†, hÏ†, rflâŸ©]

end comm_map

section integerLattice

open Module FiniteDimensional

/-- A `â„`-basis of `â„^râ‚ Ã— â„‚^râ‚‚` that is also a `â„¤`-basis of the image of `ğ“ K`. -/
noncomputable def latticeBasis [NumberField K] :
    Basis (Free.ChooseBasisIndex â„¤ (ğ“ K)) â„ (E K) := by
  classical
    -- We construct an `â„`-linear independent family from the image of
    -- `canonicalEmbedding.lattice_basis` by `comm_map`
    have := LinearIndependent.map (LinearIndependent.restrict_scalars
      (by { simpa only [Complex.real_smul, mul_one] using Complex.ofReal_injective })
      (canonicalEmbedding.latticeBasis K).linearIndependent)
      (disjoint_span_comm_map_ker K)
    -- and it's a basis since it has the right cardinality
    refine basisOfLinearIndependentOfCardEqFinrank this ?_
    rw [â† finrank_eq_card_chooseBasisIndex, RingOfIntegers.rank, finrank_prod, finrank_pi,
      finrank_pi_fintype, Complex.finrank_real_complex, Finset.sum_const, Finset.card_univ,
      â† card_real_embeddings, Algebra.id.smul_eq_mul, mul_comm, â† card_complex_embeddings,
      â† NumberField.Embeddings.card K â„‚, Fintype.card_subtype_compl,
      Nat.add_sub_of_le (Fintype.card_subtype_le _)]

@[simp]
theorem latticeBasis_apply [NumberField K] (i : Free.ChooseBasisIndex â„¤ (ğ“ K)) :
    latticeBasis K i = (mixedEmbedding K) (integralBasis K i) := by
  simp only [latticeBasis, coe_basisOfLinearIndependentOfCardEqFinrank, Function.comp_apply,
    canonicalEmbedding.latticeBasis_apply, integralBasis_apply, comm_map_canonical_eq_mixed]

theorem mem_span_latticeBasis [NumberField K] (x : (E K)) :
    x âˆˆ Submodule.span â„¤ (Set.range (latticeBasis K)) â†” x âˆˆ mixedEmbedding K '' (ğ“ K) := by
  rw [show Set.range (latticeBasis K) =
      (mixedEmbedding K).toIntAlgHom.toLinearMap '' (Set.range (integralBasis K)) by
    rw [â† Set.range_comp]; exact congrArg Set.range (funext (fun i => latticeBasis_apply K i))]
  rw [â† Submodule.map_span, â† SetLike.mem_coe, Submodule.map_coe]
  rw [show (Submodule.span â„¤ (Set.range (integralBasis K)) : Set K) = ğ“ K by
    ext; exact mem_span_integralBasis K]
  rfl

end integerLattice

section convex_body

open Metric ENNReal NNReal

variable (f : InfinitePlace K â†’ â„â‰¥0)

/-- The convex body defined by `f`: the set of points `x : E` such that `â€–x wâ€– < f w` for all
infinite places `w`. -/
def convex_body : Set (E K) :=
  (Set.pi Set.univ (fun w : { w : InfinitePlace K // IsReal w } => ball 0 (f w))) Ã—Ë¢
  (Set.pi Set.univ (fun w : { w : InfinitePlace K // IsComplex w } => ball 0 (f w)))

theorem convex_body_mem {x : K} :
    mixedEmbedding K x âˆˆ (convex_body K f) â†” âˆ€ w : InfinitePlace K, w x < f w := by
  simp_rw [mixedEmbedding, RingHom.prod_apply, convex_body, Set.mem_prod, Set.mem_pi,
    Set.mem_univ, forall_true_left, mem_ball_zero_iff, Pi.ringHom_apply, â† Complex.norm_real,
    embedding_of_isReal_apply, Subtype.forall, â† ball_or_left, â† not_isReal_iff_isComplex, em,
    forall_true_left, norm_embedding_eq]

theorem convex_body_symmetric (x : E K) (hx : x âˆˆ (convex_body K f)) :
    -x âˆˆ (convex_body K f) := by
  simp only [convex_body, Set.mem_prod, Prod.fst_neg, Set.mem_pi, Set.mem_univ, Pi.neg_apply,
    mem_ball_zero_iff, norm_neg, Real.norm_eq_abs, forall_true_left, Subtype.forall,
    Prod.snd_neg, Complex.norm_eq_abs, hx] at hx âŠ¢
  exact hx

theorem convex_body_convex :
    Convex â„ (convex_body K f) :=
  Convex.prod (convex_pi (fun _ _ => convex_ball _ _)) (convex_pi (fun _ _ => convex_ball _ _))

open Classical Fintype MeasureTheory MeasureTheory.Measure BigOperators

-- See: https://github.com/leanprover/lean4/issues/2220
local macro_rules | `($x ^ $y)   => `(HPow.hPow $x $y)

variable [NumberField K]

/-- The fudge factor that appears in the formula for the volume of `convex_body`. -/
noncomputable def constant_factor : â„â‰¥0âˆ :=
  (2 : â„â‰¥0âˆ) ^ card {w : InfinitePlace K // IsReal w} *
    volume (ball (0 : â„‚) 1) ^ card {w : InfinitePlace K // IsComplex w}

instance : IsAddHaarMeasure (@volume â„‚ Complex.measureSpace) := MapLinearEquiv.isAddHaarMeasure _ _

theorem constant_factor_pos : 0 < (constant_factor K) := by
  refine mul_pos (NeZero.ne _) ?_
  exact ENNReal.pow_ne_zero (ne_of_gt (measure_ball_pos _ _ (by norm_num))) _

theorem constant_factor_lt_top : (constant_factor K) < âŠ¤ := by
  refine mul_lt_top ?_ ?_
  Â· exact ne_of_lt (pow_lt_top (lt_top_iff_ne_top.mpr two_ne_top) _)
  Â· exact ne_of_lt (pow_lt_top measure_ball_lt_top _)

set_option maxHeartbeats 400000 in
/-- The volume of `(convex_body K f)` where `convex_body K f` is the set of points `x` such that
`â€–x wâ€– < f w` for all infinite places `w`. -/
theorem convex_body_volume :
    volume (convex_body K f) = (constant_factor K) * âˆ w, (f w) ^ (mult w) := by
  rw [volume_eq_prod, convex_body, prod_prod, volume_pi, volume_pi, pi_pi, pi_pi]
  conv_lhs =>
    congr; congr; next => skip
    ext
    rw [Real.volume_ball, ofReal_mul (by norm_num), ofReal_coe_nnreal, mul_comm]
  conv_lhs =>
    congr; next => skip
    congr; next => skip
    ext i
    rw [addHaar_ball _ _ (by exact (f i).prop), Complex.finrank_real_complex, â† NNReal.coe_pow,
      ofReal_coe_nnreal, mul_comm]
  rw [Finset.prod_mul_distrib, Finset.prod_mul_distrib, Finset.prod_const, Finset.prod_const,
    Finset.card_univ, Finset.card_univ, mul_assoc, mul_comm, â† mul_assoc, mul_assoc, ofReal_ofNat,
    â† constant_factor]
  simp_rw [mult, pow_ite, pow_one]
  rw [Finset.prod_ite]
  simp_rw [coe_mul, coe_finset_prod]
  simp_rw [show (fun w : InfinitePlace K â†¦ Â¬IsReal w) = (fun w â†¦ IsComplex w)
    by funext; rw [not_isReal_iff_isComplex]]
  congr 1; rw [mul_comm]; congr 1
  all_goals
  Â· rw [â† Finset.prod_subtype_eq_prod_filter]
    congr; ext
    exact âŸ¨fun _ =>  Finset.mem_subtype.mpr (Finset.mem_univ _), fun _ => Finset.mem_univ _âŸ©

variable {f}

/-- This is a technical result: quite often, we want to impose conditions at all infinite places
but one and choose the value at the remaining place so that we can apply
`exists_ne_zero_mem_ring_of_integers_lt`. -/
theorem adjust_f {wâ‚ : InfinitePlace K} (B : â„â‰¥0) (hf : âˆ€ w, w â‰  wâ‚â†’ f w â‰  0) :
    âˆƒ g : InfinitePlace K â†’ â„â‰¥0, (âˆ€ w, w â‰  wâ‚ â†’ g w = f w) âˆ§ âˆ w, (g w) ^ mult w = B := by
  let S := âˆ w in Finset.univ.erase wâ‚, (f w) ^ mult w
  refine âŸ¨Function.update f wâ‚ ((B * Sâ»Â¹) ^ (mult wâ‚ : â„)â»Â¹), ?_, ?_âŸ©
  Â· exact fun w hw => Function.update_noteq hw _ f
  Â· rw [â† Finset.mul_prod_erase Finset.univ _ (Finset.mem_univ wâ‚), Function.update_same,
      Finset.prod_congr rfl fun w hw => by rw [Function.update_noteq (Finset.ne_of_mem_erase hw)],
      â† NNReal.rpow_nat_cast, â† NNReal.rpow_mul, inv_mul_cancel, NNReal.rpow_one, mul_assoc,
      inv_mul_cancel, mul_one]
    Â· rw [Finset.prod_ne_zero_iff]
      exact fun w hw => pow_ne_zero _ (hf w (Finset.ne_of_mem_erase hw))
    Â· rw [mult]; split_ifs <;> norm_num

end convex_body

section minkowski

open MeasureTheory MeasureTheory.Measure Classical ENNReal FiniteDimensional

variable [NumberField K]

/-- The bound that appears in Minkowski theorem, see
`MeasureTheory.exists_ne_zero_mem_lattice_of_measure_mul_two_pow_lt_measure`. -/
noncomputable def minkowski_bound : â„â‰¥0âˆ :=
  volume (Zspan.fundamentalDomain (latticeBasis K)) * 2 ^ (finrank â„ (E K))

theorem minkowski_bound_lt_top : minkowski_bound K < âŠ¤ := by
  refine mul_lt_top ?_ ?_
  Â· exact ne_of_lt (Zspan.fundamentalDomain_bounded (latticeBasis K)).measure_lt_top
  Â· exact ne_of_lt (pow_lt_top (lt_top_iff_ne_top.mpr two_ne_top) _)

/-- Assume that `f : InfinitePlace K â†’ â„â‰¥0` is such that
`minkowski_bound K < volume (convex_body K f)` where `convex_body K f` is the set of points `x`
such that `â€–x wâ€– < f w` for all infinite places `w` (see `convex_body_volume` for the computation
of this volume), then there exists a nonzero algebraic integer `a` in `ğ“ K` such that
`w a < f w` for all infinite places `w`. -/
theorem exists_ne_zero_mem_ringOfIntegers_lt (h : minkowski_bound K < volume (convex_body K f)) :
    âˆƒ (a : ğ“ K), a â‰  0 âˆ§ âˆ€ w : InfinitePlace K, w a < f w := by
  have : @IsAddHaarMeasure (E K) _ _ _ volume := prod.instIsAddHaarMeasure volume volume
  have h_fund := Zspan.isAddFundamentalDomain (latticeBasis K) volume
  have : Countable (Submodule.span â„¤ (Set.range (latticeBasis K))).toAddSubgroup := by
    change Countable (Submodule.span â„¤ (Set.range (latticeBasis K)): Set (E K))
    infer_instance
  obtain âŸ¨âŸ¨x, hxâŸ©, h_nzr, h_memâŸ© := exists_ne_zero_mem_lattice_of_measure_mul_two_pow_lt_measure
    h_fund h (convex_body_symmetric K f) (convex_body_convex K f)
  rw [Submodule.mem_toAddSubgroup, mem_span_latticeBasis] at hx
  obtain âŸ¨a, ha, rflâŸ© := hx
  refine âŸ¨âŸ¨a, haâŸ©, ?_, (convex_body_mem K f).mp h_memâŸ©
  rw [ne_eq, AddSubgroup.mk_eq_zero_iff, map_eq_zero, â† ne_eq] at h_nzr
  exact Subtype.ne_of_val_ne h_nzr

end minkowski

end NumberField.mixedEmbedding
