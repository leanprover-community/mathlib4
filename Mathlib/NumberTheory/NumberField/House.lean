/-
Copyright (c) 2024 Michail Karatarakis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michail Karatarakis
-/
import Mathlib.Analysis.Matrix
import Mathlib.NumberTheory.NumberField.CanonicalEmbedding.Basic

/-!
# House of an algebraic number
This file defines the house of an algebraic number `Î±`, which is
the largest modulus of its conjugates.

## References
* [D. Marcus, *Number Fields*][marcus1977number]
* [Keng, H. L, *Introduction to number theory*][keng1982house]

## Tags
number field, algebraic number, house
-/

variable {K : Type*} [Field K] [NumberField K]

noncomputable section

open Module.Free FiniteDimensional NumberField.canonicalEmbedding NumberField

/-- The house of an algebraic number as the norm of its image by the canonical embedding.-/
abbrev House (Î± : K) : â„ := â€–canonicalEmbedding K Î±â€–

theorem House_eq_sup' (Î± : K) :
    House Î± = Finset.univ.sup' Finset.univ_nonempty (fun Ï† : K â†’+* â„‚ â†¦ â€–Ï† Î±â€–â‚Š) := by
  rw [House, â† coe_nnnorm, nnnorm_eq, â† Finset.sup'_eq_sup Finset.univ_nonempty]

variable (K)

/-- An equivalence between the set of embeddings of `K` into `â„‚` and the index set of the chosen
  basis of the ring of integers of `K`. -/
abbrev equivReindex : (K â†’+* â„‚) â‰ƒ (ChooseBasisIndex â„¤ (ğ“ K)) := by
  refine Fintype.equivOfCardEq ?_
  rw [Embeddings.card, â† finrank_eq_card_chooseBasisIndex, RingOfIntegers.rank]

/-- The basis matrix for the embeddings of `K` into `â„‚`. This matrix is formed by
  taking the lattice basis vectors of `K` and reindexing them according to the
  equivalence `equivReindex`, then transposing the resulting matrix. -/
abbrev basisMatrix : Matrix (K â†’+* â„‚) (K â†’+* â„‚) â„‚ :=
  (Matrix.of fun i â†¦ latticeBasis K (equivReindex K i)).transpose

theorem canonicalEmbedding.mem_rat_span_latticeBasis (x : K) :
    (canonicalEmbedding K) x âˆˆ Submodule.span â„š (Set.range (latticeBasis K)) := by
  rw [â† Basis.sum_repr (integralBasis K) x, map_sum]
  simp_rw [map_rat_smul]
  refine Submodule.sum_smul_mem _ _ (fun i _ â†¦ Submodule.subset_span ?_)
  rw [â† latticeBasis_apply]
  exact Set.mem_range_self i

theorem canonicalEmbedding.integralBasis_repr_apply (x : K) (i : ChooseBasisIndex â„¤ (ğ“ K)) :
    (canonicalEmbedding.latticeBasis K).repr (canonicalEmbedding K x) i =
      (integralBasis K).repr x i := by
  rw [â† Basis.restrictScalars_repr_apply â„š _ âŸ¨_, mem_rat_span_latticeBasis K xâŸ©, eq_ratCast,
    Rat.cast_inj]
  let f := (canonicalEmbedding K).toRatAlgHom.toLinearMap.codRestrict _
    (fun x â†¦ mem_rat_span_latticeBasis K x)
  suffices ((latticeBasis K).restrictScalars â„š).repr.toLinearMap âˆ˜â‚— f =
    (integralBasis K).repr.toLinearMap from DFunLike.congr_fun (LinearMap.congr_fun this x) i
  refine Basis.ext (integralBasis K) (fun i â†¦ ?_)
  have : f (integralBasis K i) = ((latticeBasis K).restrictScalars â„š) i := by
    apply Subtype.val_injective
    rw [LinearMap.codRestrict_apply, AlgHom.toLinearMap_apply, Basis.restrictScalars_apply,
      latticeBasis_apply]
    rfl
  simp_rw [LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply, this, Basis.repr_self]

instance : DecidableEq (K â†’+* â„‚) := Classical.typeDecidableEq (K â†’+* â„‚)

theorem det_of_basisMatrix_non_zero : (basisMatrix K).transpose.det â‰  0 := by
      let e : (K â†’+* â„‚) â‰ƒ ChooseBasisIndex â„¤ (ğ“ K) := equivReindex K
      let N := Algebra.embeddingsMatrixReindex â„š â„‚ (fun i => integralBasis K (e i))
         RingHom.equivRatAlgHom
      rw [show (basisMatrix K).transpose = N by {
        ext:2; simp only [N, Matrix.transpose_apply, latticeBasis_apply, integralBasis_apply,
        Matrix.of_apply, apply_at]; rfl}, â† pow_ne_zero_iff two_ne_zero]
      convert (map_ne_zero_iff _ (algebraMap â„š â„‚).injective).mpr
        (Algebra.discr_not_zero_of_basis â„š (integralBasis K))
      rw [â† Algebra.discr_reindex â„š (integralBasis K) e.symm]
      exact (Algebra.discr_eq_det_embeddingsMatrixReindex_pow_two â„š â„‚
        (fun i => integralBasis K (e i)) RingHom.equivRatAlgHom).symm

instance : Invertible (basisMatrix K) := by
    have :(basisMatrix K).transpose.det â‰  0 := det_of_basisMatrix_non_zero K
    rw [Matrix.det_transpose (basisMatrix K)] at this
    exact Matrix.invertibleOfIsUnitDet _ (Ne.isUnit this)

/-- `c` is defined as the product of the maximum absolute value of the entries of the
  inverse of the matrix `basisMatrix` and  `(finrank â„š K)`. -/
def c := (finrank â„š K) * â€–fun i j => (basisMatrix K)â»Â¹ i jâ€–

theorem basis_repr_abs_le_const_mul_house (Î± : ğ“ K) :
    âˆ€ i, Complex.abs (((integralBasis K).reindex (equivReindex K).symm).repr Î± i : â„‚) â‰¤
    @c K _ _ * House (algebraMap (ğ“ K) K Î±) := fun i => calc

   _ = Complex.abs (âˆ‘ j, (basisMatrix  K)â»Â¹ i j *
        (canonicalEmbedding K (algebraMap (ğ“ K) K Î±) j)) := by
      have : canonicalEmbedding K Î± = (basisMatrix K).mulVec (fun i â†¦
         (((integralBasis K).reindex (equivReindex K).symm).repr Î± i : â„‚)) := by
        ext
        rw [â† (latticeBasis K).sum_repr (canonicalEmbedding K Î±),
            â† Equiv.sum_comp (equivReindex K)]
        simp_rw [canonicalEmbedding.integralBasis_repr_apply, Matrix.mulVec, Matrix.dotProduct,
          basisMatrix, Matrix.transpose_apply, Matrix.of_apply, Finset.sum_apply,
          mul_comm, Basis.repr_reindex, Finsupp.mapDomain_equiv_apply, Equiv.symm_symm,
          Pi.smul_apply, smul_eq_mul]
      have : (basisMatrix K)â»Â¹.mulVec (fun j => canonicalEmbedding K (algebraMap (ğ“ K) K Î±) j) i =
        ((integralBasis K).reindex (equivReindex K).symm).repr Î± i := by
        {rw [Matrix.inv_mulVec_eq_vec (basisMatrix  K) this]}
      rw [â† this]; rfl

    _ â‰¤ âˆ‘ j, â€–fun i j => (basisMatrix K)â»Â¹ i jâ€– *
         Complex.abs (canonicalEmbedding K (algebraMap (ğ“ K) K Î±) j) := by
           trans
           Â·  trans
              Â· apply AbsoluteValue.sum_le Complex.abs
              Â· apply Finset.sum_le_sum (fun _ _ => (AbsoluteValue.map_mul Complex.abs _ _).le)
           Â· apply Finset.sum_le_sum
              (fun _ _ =>  mul_le_mul_of_nonneg_right ?_ (AbsoluteValue.nonneg Complex.abs _))
             Â· rw [â† Complex.norm_eq_abs]
               exact Matrix.norm_entry_le_entrywise_sup_norm (basisMatrix K)â»Â¹

    _ â‰¤ âˆ‘ _, â€–fun i j => (basisMatrix K)â»Â¹ i jâ€– * House  (algebraMap (ğ“ K) K Î±) := by
          apply Finset.sum_le_sum
          intros j _
          apply mul_le_mul_of_nonneg_left _ (norm_nonneg fun i j â†¦ (basisMatrix K)â»Â¹ i j)
          Â· rw [â† Complex.norm_eq_abs]
            exact norm_le_pi_norm ((canonicalEmbedding K) ((algebraMap (ğ“ K) K) Î±)) j

    _ =  â†‘(finrank â„š K) * â€–fun i j => (basisMatrix K)â»Â¹ i jâ€– * House  (algebraMap (ğ“ K) K Î±) := by
          rw [Finset.sum_const, Finset.card_univ, nsmul_eq_mul, Embeddings.card, mul_assoc]

end section
