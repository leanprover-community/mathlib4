/-
Copyright (c) 2026 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
module

public import Mathlib.Data.ZMod.Units
public import Mathlib.NumberTheory.Cyclotomic.Gal

/-!
# Galois theory for cyclotomic fields

In this file, we study the Galois theory of cyclotomic extensions of `â„š`.

## Main definitions and results

- `IsCyclotomicExtension.Rat.galEquivZMod`: the isomorphism between `Gal(â„š(Î¶â‚™)/â„š)` and `(â„¤/nâ„¤)Ë£`
  that sends `Ïƒ` to the class `a` such that `Ïƒ (Î¶â‚™) = Î¶â‚™ ^ a`.

-/

@[expose] public section

namespace IsCyclotomicExtension.Rat

open NumberField IsCyclotomicExtension

variable (n : â„•) [NeZero n] (K : Type*) [Field K] [NumberField K]
  [hK : IsCyclotomicExtension {n} â„š K]

include hK in
/--
The isomorphism between `Gal(â„š(Î¶â‚™)/â„š)` and `(â„¤/nâ„¤)Ë£` that sends `Ïƒ` to the class `a` such that
`Ïƒ (Î¶â‚™) = Î¶â‚™ ^ a`.
-/
noncomputable def galEquivZMod : Gal(K/â„š) â‰ƒ* (ZMod n)Ë£ :=
  IsCyclotomicExtension.autEquivPow K <| Polynomial.cyclotomic.irreducible_rat (NeZero.pos n)

theorem galEquivZMod_apply_of_pow_eq (Ïƒ : Gal(K/â„š)) {x : K} (hx : x ^ n = 1) :
    Ïƒ x = x ^ (galEquivZMod n K Ïƒ).val.val := by
  obtain âŸ¨a, -, rflâŸ© := (zeta_spec n â„š K).eq_pow_of_pow_eq_one hx
  rw [map_pow, pow_right_comm, galEquivZMod, autEquivPow_apply, OneHom.toFun_eq_coe,
    MonoidHom.toOneHom_coe, IsPrimitiveRoot.autToPow_spec]

theorem galEquivZMod_smul_of_pow_eq (Ïƒ : Gal(K/â„š)) {x : ð“ž K} (hx : x ^ n = 1) :
    Ïƒ â€¢ x = x ^ (galEquivZMod n K Ïƒ).val.val := by
  apply FaithfulSMul.algebraMap_injective (ð“ž K) K
  apply galEquivZMod_apply_of_pow_eq n K Ïƒ <| by rw [â† Subalgebra.coe_pow, hx, OneMemClass.coe_one]

section restrict

variable {m : â„•} [NeZero m] (F : Type*) [Field F] [NumberField F]
  [hF : IsCyclotomicExtension {m} â„š F] [Algebra F K] [IsGalois â„š F]

/--
Let `m âˆ£ n`. Then, the following diagram commutes:
Gal(â„š(Î¶â‚™)/â„š) â†’ (â„¤/nâ„¤)Ë£
  â†“              â†“
Gal(â„š(Î¶â‚˜)/â„š) â†’ (â„¤/mâ„¤)Ë£
where the horizontal maps are `galEquivZMod`, the left map is the restriction map and the right map
is the natural map.
-/
theorem galEquivZMod_restrictNormal_apply (h : m âˆ£ n) (Ïƒ : Gal(K/â„š)) :
    galEquivZMod m F (Ïƒ.restrictNormal F) = ZMod.unitsMap h (galEquivZMod n K Ïƒ) := by
  have hÎ¶ := IsCyclotomicExtension.zeta_spec m â„š F
  set Î¶ := IsCyclotomicExtension.zeta m â„š F
  suffices Î¶ ^ (galEquivZMod m F (Ïƒ.restrictNormal F)).val.val = Î¶ ^ (galEquivZMod n K Ïƒ).val.val by
    rw [(hÎ¶.isOfFinOrder (NeZero.ne _)).pow_inj_mod, â† hÎ¶.eq_orderOf,
      â† ZMod.natCast_eq_natCast_iff', ZMod.natCast_val, ZMod.natCast_val, ZMod.cast_id] at this
    rwa [Units.ext_iff]
  apply FaithfulSMul.algebraMap_injective F K
  rw [map_pow, map_pow, â† galEquivZMod_apply_of_pow_eq, â† AlgEquiv.restrictNormal_commutes,
      galEquivZMod_apply_of_pow_eq m _ _ hÎ¶.pow_eq_one, map_pow]
  rw [â† map_pow, (hÎ¶.pow_eq_one_iff_dvd _).mpr h, map_one]

end restrict

end IsCyclotomicExtension.Rat
