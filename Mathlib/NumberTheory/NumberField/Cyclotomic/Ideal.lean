/-
Copyright (c) 2025 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
module

public import Mathlib.NumberTheory.NumberField.Cyclotomic.Basic
public import Mathlib.NumberTheory.NumberField.Ideal.KummerDedekind
public import Mathlib.RingTheory.Polynomial.Cyclotomic.Factorization
public import Mathlib.RingTheory.RootsOfUnity.CyclotomicUnits

/-!
# Ideals in cyclotomic fields

In this file, we prove results about ideals in cyclotomic extensions of `â„š`.

## Main results

* `IsCyclotomicExtension.Rat.ncard_primesOver_of_prime_pow`: there is only one prime ideal above
  the prime `p` in `â„š(Î¶_páµ)`

* `IsCyclotomicExtension.Rat.inertiaDeg_eq_of_prime_pow`: the residual degree of the prime ideal
  above `p` in `â„š(Î¶_páµ)` is `1`.

* `IsCyclotomicExtension.Rat.ramificationIdx_eq_of_prime_pow`: the ramification index of the prime
  ideal above `p` in `â„š(Î¶_páµ)` is `p ^ (k - 1) * (p - 1)`.

* `IsCyclotomicExtension.Rat.inertiaDeg_of_not_dvd`: if the prime `p` does not divide `m`, then
  the inertia degree of `p` in `â„š(Î¶â‚˜)` is the order of `p` modulo `m`.

* `IsCyclotomicExtension.Rat.ramificationIdx_of_not_dvd`: if the prime `p` does not divide `m`,
  then the ramification index of `p` in `â„š(Î¶â‚˜)` is `1`.

-/

@[expose] public section

namespace IsCyclotomicExtension.Rat

open Ideal NumberField RingOfIntegers

variable (m p k : â„•) [hp : Fact (Nat.Prime p)] (K : Type*) [Field K] [NumberField K]
  (P : Ideal (ğ“ K)) [hPâ‚ : P.IsPrime] [hPâ‚‚ : P.LiesOver (Ideal.span {(p : â„¤)})]

local notation3 "ğ’‘" => (Ideal.span {(p : â„¤)})

section PrimePow

variable {K} [hK : IsCyclotomicExtension {p ^ (k + 1)} â„š K] {Î¶ : K}
  (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (k + 1)))

instance isPrime_span_zeta_sub_one : IsPrime (span {hÎ¶.toInteger - 1}) := by
  rw [Ideal.span_singleton_prime]
  Â· exact hÎ¶.zeta_sub_one_prime
  Â· exact Prime.ne_zero hÎ¶.zeta_sub_one_prime

theorem associated_norm_zeta_sub_one : Associated (Algebra.norm â„¤ (hÎ¶.toInteger - 1)) (p : â„¤) := by
  by_cases h : p = 2
  Â· cases k with
    | zero =>
      rw [h, zero_add, pow_one] at hK hÎ¶
      rw [hÎ¶.norm_toInteger_sub_one_of_eq_two, h, Int.ofNat_two, Associated.neg_left_iff]
    | succ n =>
      rw [h, add_assoc, one_add_one_eq_two] at hK hÎ¶
      rw [hÎ¶.norm_toInteger_sub_one_of_eq_two_pow, h, Int.ofNat_two]
  Â· rw [hÎ¶.norm_toInteger_sub_one_of_prime_ne_two h]

theorem absNorm_span_zeta_sub_one : absNorm (span {hÎ¶.toInteger - 1}) = p := by
  simpa using congr_arg absNorm <|
    span_singleton_eq_span_singleton.mpr <| associated_norm_zeta_sub_one p k hÎ¶

theorem p_mem_span_zeta_sub_one : (p : ğ“ K) âˆˆ span {hÎ¶.toInteger - 1} := by
  convert Ideal.absNorm_mem _
  exact (absNorm_span_zeta_sub_one ..).symm

theorem span_zeta_sub_one_ne_bot : span {hÎ¶.toInteger - 1} â‰  âŠ¥ :=
  (Submodule.ne_bot_iff _).mpr âŸ¨p, p_mem_span_zeta_sub_one p k hÎ¶, NeZero.natCast_ne p (ğ“ K)âŸ©

instance liesOver_span_zeta_sub_one : (span {hÎ¶.toInteger - 1}).LiesOver ğ’‘ := by
  rw [liesOver_iff]
  refine Ideal.IsMaximal.eq_of_le (Int.ideal_span_isMaximal_of_prime p) IsPrime.ne_top' ?_
  rw [span_singleton_le_iff_mem, mem_comap, algebraMap_int_eq, map_natCast]
  exact p_mem_span_zeta_sub_one p k hÎ¶

theorem inertiaDeg_span_zeta_sub_one : inertiaDeg ğ’‘ (span {hÎ¶.toInteger - 1}) = 1 := by
  have := liesOver_span_zeta_sub_one p k hÎ¶
  rw [â† Nat.pow_right_inj hp.out.one_lt, pow_one, â† absNorm_eq_pow_inertiaDeg' _ hp.out,
    absNorm_span_zeta_sub_one]

attribute [local instance] FractionRing.liftAlgebra in
theorem map_eq_span_zeta_sub_one_pow :
    (map (algebraMap â„¤ (ğ“ K)) ğ’‘) = span {hÎ¶.toInteger - 1} ^ Module.finrank â„š K := by
  have : IsGalois â„š K := isGalois {p ^ (k + 1)} â„š K
  have : IsGalois (FractionRing â„¤) (FractionRing (ğ“ K)) := by
    refine IsGalois.of_equiv_equiv (f := (FractionRing.algEquiv â„¤ â„š).toRingEquiv.symm)
      (g := (FractionRing.algEquiv (ğ“ K) K).toRingEquiv.symm) <|
        RingHom.ext fun x â†¦ IsFractionRing.algEquiv_commutes (FractionRing.algEquiv â„¤ â„š).symm
          (FractionRing.algEquiv (ğ“ K) K).symm _
  rw [map_span, Set.image_singleton, span_singleton_eq_span_singleton.mpr
    ((associated_norm_zeta_sub_one p k hÎ¶).symm.map (algebraMap â„¤ (ğ“ K))),
    â† Algebra.intNorm_eq_norm, Algebra.algebraMap_intNorm_of_isGalois, â† prod_span_singleton]
  conv_lhs =>
    enter [2, Ïƒ]
    rw [span_singleton_eq_span_singleton.mpr
      (hÎ¶.toInteger_isPrimitiveRoot.associated_sub_one_map_sub_one Ïƒ).symm]
  rw [Finset.prod_const, Finset.card_univ, â† Fintype.card_congr (galRestrict â„¤ â„š K (ğ“ K)).toEquiv,
    â† Nat.card_eq_fintype_card, IsGalois.card_aut_eq_finrank]

theorem ramificationIdx_span_zeta_sub_one :
    ramificationIdx (algebraMap â„¤ (ğ“ K)) ğ’‘ (span {hÎ¶.toInteger - 1}) = p ^ k * (p - 1) := by
  have := liesOver_span_zeta_sub_one p k hÎ¶
  have h := isPrime_span_zeta_sub_one p k hÎ¶
  rw [â† Nat.totient_prime_pow_succ hp.out, â† finrank _ K,
    IsDedekindDomain.ramificationIdx_eq_multiplicity _ h, map_eq_span_zeta_sub_one_pow p k hÎ¶,
    multiplicity_pow_self (span_zeta_sub_one_ne_bot p k hÎ¶) (isUnit_iff.not.mpr h.ne_top)]
  exact map_ne_bot_of_ne_bot <| by simpa using hp.out.ne_zero

variable (K)

include hK in
theorem ncard_primesOver_of_prime_pow :
    (primesOver ğ’‘ (ğ“ K)).ncard = 1 := by
  have : IsGalois â„š K := isGalois {p ^ (k + 1)} â„š K
  have : ğ’‘ â‰  âŠ¥ := by simpa using hp.out.ne_zero
  have h_main := ncard_primesOver_mul_ramificationIdxIn_mul_inertiaDegIn this (ğ“ K) (K â‰ƒâ‚[â„š] K)
  have hÎ¶ := hK.zeta_spec
  have := liesOver_span_zeta_sub_one p k hÎ¶
  rwa [ramificationIdxIn_eq_ramificationIdx ğ’‘ (span {hÎ¶.toInteger - 1}) (K â‰ƒâ‚[â„š] K),
    inertiaDegIn_eq_inertiaDeg ğ’‘ (span {hÎ¶.toInteger - 1}) (K â‰ƒâ‚[â„š] K),
    inertiaDeg_span_zeta_sub_one,
    ramificationIdx_span_zeta_sub_one, mul_one, â† Nat.totient_prime_pow_succ hp.out,
    â† finrank _ K, IsGaloisGroup.card_eq_finrank (K â‰ƒâ‚[â„š] K) â„š K, Nat.mul_eq_right] at h_main
  exact Module.finrank_pos.ne'

theorem eq_span_zeta_sub_one_of_liesOver (P : Ideal (ğ“ K)) [hPâ‚ : P.IsPrime] [hPâ‚‚ : P.LiesOver ğ’‘] :
    P = span {hÎ¶.toInteger - 1} := by
  have : P âˆˆ primesOver ğ’‘ (ğ“ K) := âŸ¨hPâ‚, hPâ‚‚âŸ©
  have : span {hÎ¶.toInteger - 1} âˆˆ primesOver ğ’‘ (ğ“ K) :=
    âŸ¨isPrime_span_zeta_sub_one p k hÎ¶, liesOver_span_zeta_sub_one p k hÎ¶âŸ©
  have := ncard_primesOver_of_prime_pow p k K
  aesop

include hK in
theorem inertiaDeg_eq_of_prime_pow (P : Ideal (ğ“ K)) [hPâ‚ : P.IsPrime] [hPâ‚‚ : P.LiesOver ğ’‘] :
    inertiaDeg ğ’‘ P = 1 := by
  rw [eq_span_zeta_sub_one_of_liesOver p k K hK.zeta_spec P, inertiaDeg_span_zeta_sub_one]

include hK in
theorem ramificationIdx_eq_of_prime_pow (P : Ideal (ğ“ K)) [hPâ‚ : P.IsPrime] [hPâ‚‚ : P.LiesOver ğ’‘] :
    ramificationIdx (algebraMap â„¤ (ğ“ K)) ğ’‘ P = p ^ k * (p - 1) := by
  rw [eq_span_zeta_sub_one_of_liesOver p k K hK.zeta_spec P, ramificationIdx_span_zeta_sub_one]

end PrimePow

section Prime

variable {K} [hK : IsCyclotomicExtension {p} â„š K] {Î¶ : K} (hÎ¶ : IsPrimitiveRoot Î¶ p)

instance isPrime_span_zeta_sub_one' : IsPrime (span {hÎ¶.toInteger - 1}) := by
  rw [â† pow_one p] at hK hÎ¶
  exact isPrime_span_zeta_sub_one p 0 hÎ¶

theorem inertiaDeg_span_zeta_sub_one' : inertiaDeg ğ’‘ (span {hÎ¶.toInteger - 1}) = 1 := by
  rw [â† pow_one p] at hK hÎ¶
  exact inertiaDeg_span_zeta_sub_one p 0 hÎ¶

theorem ramificationIdx_span_zeta_sub_one' :
    ramificationIdx (algebraMap â„¤ (ğ“ K)) ğ’‘ (span {hÎ¶.toInteger - 1}) = p - 1 := by
  rw [â† pow_one p] at hK hÎ¶
  rw [ramificationIdx_span_zeta_sub_one p 0 hÎ¶, pow_zero, one_mul]

variable (K)

include hK in
theorem ncard_primesOver_of_prime :
    (primesOver ğ’‘ (ğ“ K)).ncard = 1 := by
  rw [â† pow_one p] at hK
  exact ncard_primesOver_of_prime_pow p 0 K

theorem eq_span_zeta_sub_one_of_liesOver' (P : Ideal (ğ“ K)) [hPâ‚ : P.IsPrime] [hPâ‚‚ : P.LiesOver ğ’‘] :
    P = span {hÎ¶.toInteger - 1} := by
  rw [â† pow_one p] at hK hÎ¶
  exact eq_span_zeta_sub_one_of_liesOver p 0 K hÎ¶ P

include hK in
theorem inertiaDeg_eq_of_prime (P : Ideal (ğ“ K)) [hPâ‚ : P.IsPrime] [hPâ‚‚ : P.LiesOver ğ’‘] :
    inertiaDeg ğ’‘ P = 1 := by
  rw [eq_span_zeta_sub_one_of_liesOver' p K hK.zeta_spec P, inertiaDeg_span_zeta_sub_one']

include hK in
theorem ramificationIdx_eq_of_prime (P : Ideal (ğ“ K)) [hPâ‚ : P.IsPrime] [hPâ‚‚ : P.LiesOver ğ’‘] :
    ramificationIdx (algebraMap â„¤ (ğ“ K)) ğ’‘ P = p - 1 := by
  rw [eq_span_zeta_sub_one_of_liesOver' p K hK.zeta_spec P, ramificationIdx_span_zeta_sub_one']

end Prime

section notDvd

open NumberField.Ideal Polynomial

variable {m} [NeZero m] [hK : IsCyclotomicExtension {m} â„š K]

theorem inertiaDeg_of_not_dvd (hm : Â¬ p âˆ£ m) :
    inertiaDeg ğ’‘ P = orderOf (p : ZMod m) := by
  replace hm : p.Coprime m := not_not.mp <| (Nat.Prime.dvd_iff_not_coprime hp.out).not.mp hm
  let Î¶ := (zeta_spec m â„š K).toInteger
  have hâ‚ : Â¬ p âˆ£ exponent Î¶ := by
    rw [exponent_eq_one_iff.mpr <| adjoin_singleton_eq_top (zeta_spec m â„š K)]
    exact hp.out.not_dvd_one
  have hâ‚‚ := (primesOverSpanEquivMonicFactorsMod hâ‚ âŸ¨P, âŸ¨inferInstance, inferInstanceâŸ©âŸ©).2
  have hâ‚ƒ := inertiaDeg_primesOverSpanEquivMonicFactorsMod_symm_apply' hâ‚ hâ‚‚
  simp only [Subtype.coe_eta, Equiv.symm_apply_apply] at hâ‚ƒ
  rw [Multiset.mem_toFinset, Polynomial.mem_normalizedFactors_iff
    (map_monic_ne_zero (minpoly.monic Î¶.isIntegral))] at hâ‚‚
  rw [hâ‚ƒ, natDegree_of_dvd_cyclotomic_of_irreducible (by simp) hm (f := 1) _ hâ‚‚.1]
  Â· simpa using (orderOf_injective _ Units.coeHom_injective (ZMod.unitOfCoprime p hm)).symm
  Â· refine dvd_trans hâ‚‚.2.2 ?_
    rw [â† map_cyclotomic_int, cyclotomic_eq_minpoly (zeta_spec m â„š K) (NeZero.pos _),
      â† (zeta_spec m â„š K).coe_toInteger, â† RingOfIntegers.minpoly_coe Î¶]
    rfl

theorem ramificationIdx_of_not_dvd (hm : Â¬ p âˆ£ m) :
    ramificationIdx (algebraMap â„¤ (ğ“ K)) ğ’‘ P = 1 := by
  let Î¶ := (zeta_spec m â„š K).toInteger
  have hâ‚ : Â¬ p âˆ£ exponent Î¶ := by
    rw [exponent_eq_one_iff.mpr <| adjoin_singleton_eq_top (zeta_spec m â„š K)]
    exact hp.out.not_dvd_one
  have hâ‚‚ := (primesOverSpanEquivMonicFactorsMod hâ‚ âŸ¨P, âŸ¨inferInstance, inferInstanceâŸ©âŸ©).2
  have hâ‚ƒ := ramificationIdx_primesOverSpanEquivMonicFactorsMod_symm_apply' hâ‚ hâ‚‚
  simp only [Subtype.coe_eta, Equiv.symm_apply_apply] at hâ‚ƒ
  rw [Multiset.mem_toFinset, Polynomial.mem_normalizedFactors_iff
    (map_monic_ne_zero (minpoly.monic Î¶.isIntegral))] at hâ‚‚
  rw [hâ‚ƒ]
  refine multiplicity_eq_of_emultiplicity_eq_some (le_antisymm ?_ ?_)
  Â· apply emultiplicity_le_one_of_separable
    Â· exact isUnit_iff_degree_eq_zero.not.mpr (Irreducible.degree_pos hâ‚‚.1).ne'
    Â· exact (zeta_spec m â„š K).toInteger_isPrimitiveRoot.separable_minpoly_mod hm
  Â· rw [ENat.coe_one]
    exact Order.one_le_iff_pos.mpr <| emultiplicity_pos_of_dvd hâ‚‚.2.2

theorem inertiaDegIn_of_not_dvd (hm : Â¬ p âˆ£ m) :
    ğ’‘.inertiaDegIn (ğ“ K) = orderOf (p : ZMod m) := by
  have : IsGalois â„š K := isGalois {m} â„š K
  obtain âŸ¨âŸ¨P, _, _âŸ©âŸ© := ğ’‘.nonempty_primesOver (S := ğ“ K)
  rw [inertiaDegIn_eq_inertiaDeg ğ’‘ P (K â‰ƒâ‚[â„š] K), inertiaDeg_of_not_dvd p K P hm]

theorem ramificationIdxIn_of_not_dvd (hm : Â¬ p âˆ£ m) :
    ğ’‘.ramificationIdxIn (ğ“ K) = 1 := by
  have : IsGalois â„š K := isGalois {m} â„š K
  obtain âŸ¨âŸ¨P, _, _âŸ©âŸ© := ğ’‘.nonempty_primesOver (S := ğ“ K)
  rw [ramificationIdxIn_eq_ramificationIdx ğ’‘ P (K â‰ƒâ‚[â„š] K), ramificationIdx_of_not_dvd p K P hm]

end notDvd

end IsCyclotomicExtension.Rat
