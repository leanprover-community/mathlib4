/-
Copyright (c) 2025 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
module

public import Mathlib.NumberTheory.NumberField.Cyclotomic.Basic
public import Mathlib.NumberTheory.NumberField.Ideal.KummerDedekind
public import Mathlib.RingTheory.Polynomial.Cyclotomic.Factorization
public import Mathlib.RingTheory.RootsOfUnity.CyclotomicUnits

/-!
# Ideals in cyclotomic fields

In this file, we prove results about ideals in cyclotomic extensions of `â„š`.

## Main results

* `IsCyclotomicExtension.Rat.ncard_primesOver_of_prime_pow`: there is only one prime ideal above
  the prime `p` in `â„š(Î¶_páµ)`

* `IsCyclotomicExtension.Rat.inertiaDeg_eq_of_prime_pow`: the residual degree of the prime ideal
  above `p` in `â„š(Î¶_páµ)` is `1`.

* `IsCyclotomicExtension.Rat.ramificationIdxIn_eq_of_prime_pow`: the ramification index of the prime
  ideal above `p` in `â„š(Î¶_páµ)` is `p ^ (k - 1) * (p - 1)`.

* `IsCyclotomicExtension.Rat.inertiaDegIn_eq_of_not_dvd`: if the prime `p` does not divide `m`, then
  the inertia degree of `p` in `â„š(Î¶â‚˜)` is the order of `p` modulo `m`.

* `IsCyclotomicExtension.Rat.ramificationIdxIn_eq_of_not_dvd`: if the prime `p` does not divide `m`,
  then the ramification index of `p` in `â„š(Î¶â‚˜)` is `1`.

* `IsCyclotomicExtension.Rat.inertiaDegIn_eq`: write `n = p ^ (k + 1) * m` where the prime `p` does
  not divide `m`, then the inertia degree of `p` in `â„š(Î¶â‚™)` is the order of `p` modulo `m`.

* `IsCyclotomicExtension.Rat.ramificationIdxIn_eq`: write `n = p ^ (k + 1) * m` where the prime `p`
  does not divide `m`, then the ramification index of `p` in `â„š(Î¶â‚™)` is `p ^ k * (p - 1)`.

-/

@[expose] public section

namespace IsCyclotomicExtension.Rat

open Ideal NumberField RingOfIntegers

variable (n m p k : â„•) [hp : Fact (Nat.Prime p)] (K : Type*) [Field K] [NumberField K]
  (P : Ideal (ğ“ K)) [hPâ‚ : P.IsPrime] [hPâ‚‚ : P.LiesOver (Ideal.span {(p : â„¤)})]

local notation3 "ğ’‘" => (Ideal.span {(p : â„¤)})

section PrimePow

variable {K} [hK : IsCyclotomicExtension {p ^ (k + 1)} â„š K] {Î¶ : K}
  (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (k + 1)))

instance isPrime_span_zeta_sub_one : IsPrime (span {hÎ¶.toInteger - 1}) := by
  rw [Ideal.span_singleton_prime]
  Â· exact hÎ¶.zeta_sub_one_prime
  Â· exact Prime.ne_zero hÎ¶.zeta_sub_one_prime

theorem associated_norm_zeta_sub_one : Associated (Algebra.norm â„¤ (hÎ¶.toInteger - 1)) (p : â„¤) := by
  by_cases h : p = 2
  Â· cases k with
    | zero =>
      rw [h, zero_add, pow_one] at hK hÎ¶
      rw [hÎ¶.norm_toInteger_sub_one_of_eq_two, h, Int.ofNat_two, Associated.neg_left_iff]
    | succ n =>
      rw [h, add_assoc, one_add_one_eq_two] at hK hÎ¶
      rw [hÎ¶.norm_toInteger_sub_one_of_eq_two_pow, h, Int.ofNat_two]
  Â· rw [hÎ¶.norm_toInteger_sub_one_of_prime_ne_two h]

theorem absNorm_span_zeta_sub_one : absNorm (span {hÎ¶.toInteger - 1}) = p := by
  simpa using congr_arg absNorm <|
    span_singleton_eq_span_singleton.mpr <| associated_norm_zeta_sub_one p k hÎ¶

theorem p_mem_span_zeta_sub_one : (p : ğ“ K) âˆˆ span {hÎ¶.toInteger - 1} := by
  convert Ideal.absNorm_mem _
  exact (absNorm_span_zeta_sub_one ..).symm

theorem span_zeta_sub_one_ne_bot : span {hÎ¶.toInteger - 1} â‰  âŠ¥ :=
  (Submodule.ne_bot_iff _).mpr âŸ¨p, p_mem_span_zeta_sub_one p k hÎ¶, NeZero.natCast_ne p (ğ“ K)âŸ©

instance liesOver_span_zeta_sub_one : (span {hÎ¶.toInteger - 1}).LiesOver ğ’‘ := by
  rw [liesOver_iff]
  refine Ideal.IsMaximal.eq_of_le (Int.ideal_span_isMaximal_of_prime p) IsPrime.ne_top' ?_
  rw [span_singleton_le_iff_mem, mem_comap, algebraMap_int_eq, map_natCast]
  exact p_mem_span_zeta_sub_one p k hÎ¶

theorem inertiaDeg_span_zeta_sub_one : inertiaDeg ğ’‘ (span {hÎ¶.toInteger - 1}) = 1 := by
  have := liesOver_span_zeta_sub_one p k hÎ¶
  rw [â† Nat.pow_right_inj hp.out.one_lt, pow_one, â† absNorm_eq_pow_inertiaDeg' _ hp.out,
    absNorm_span_zeta_sub_one]

attribute [local instance] FractionRing.liftAlgebra in
theorem map_eq_span_zeta_sub_one_pow :
    (map (algebraMap â„¤ (ğ“ K)) ğ’‘) = span {hÎ¶.toInteger - 1} ^ Module.finrank â„š K := by
  have : IsGalois â„š K := isGalois {p ^ (k + 1)} â„š K
  have : IsGalois (FractionRing â„¤) (FractionRing (ğ“ K)) := by
    refine IsGalois.of_equiv_equiv (f := (FractionRing.algEquiv â„¤ â„š).toRingEquiv.symm)
      (g := (FractionRing.algEquiv (ğ“ K) K).toRingEquiv.symm) <|
        RingHom.ext fun x â†¦ IsFractionRing.algEquiv_commutes (FractionRing.algEquiv â„¤ â„š).symm
          (FractionRing.algEquiv (ğ“ K) K).symm _
  rw [map_span, Set.image_singleton, span_singleton_eq_span_singleton.mpr
    ((associated_norm_zeta_sub_one p k hÎ¶).symm.map (algebraMap â„¤ (ğ“ K))),
    â† Algebra.intNorm_eq_norm, Algebra.algebraMap_intNorm_of_isGalois, â† prod_span_singleton]
  conv_lhs =>
    enter [2, Ïƒ]
    rw [span_singleton_eq_span_singleton.mpr
      (hÎ¶.toInteger_isPrimitiveRoot.associated_sub_one_map_sub_one Ïƒ).symm]
  rw [Finset.prod_const, Finset.card_univ, â† Fintype.card_congr (galRestrict â„¤ â„š K (ğ“ K)).toEquiv,
    â† Nat.card_eq_fintype_card, IsGalois.card_aut_eq_finrank]

theorem ramificationIdx_span_zeta_sub_one :
    ramificationIdx (algebraMap â„¤ (ğ“ K)) ğ’‘ (span {hÎ¶.toInteger - 1}) = p ^ k * (p - 1) := by
  have h := isPrime_span_zeta_sub_one p k hÎ¶
  rw [â† Nat.totient_prime_pow_succ hp.out, â† finrank _ K,
    IsDedekindDomain.ramificationIdx_eq_multiplicity _ h, map_eq_span_zeta_sub_one_pow p k hÎ¶,
    multiplicity_pow_self (span_zeta_sub_one_ne_bot p k hÎ¶) (isUnit_iff.not.mpr h.ne_top)]
  exact map_ne_bot_of_ne_bot <| by simpa using hp.out.ne_zero

variable (K)

include hK in
theorem ncard_primesOver_of_prime_pow :
    (primesOver ğ’‘ (ğ“ K)).ncard = 1 := by
  have : IsGalois â„š K := isGalois {p ^ (k + 1)} â„š K
  have : ğ’‘ â‰  âŠ¥ := by simpa using hp.out.ne_zero
  have h_main := ncard_primesOver_mul_ramificationIdxIn_mul_inertiaDegIn this (ğ“ K) Gal(K/â„š)
  have hÎ¶ := hK.zeta_spec
  have := liesOver_span_zeta_sub_one p k hÎ¶
  rwa [ramificationIdxIn_eq_ramificationIdx ğ’‘ (span {hÎ¶.toInteger - 1}) Gal(K/â„š),
    inertiaDegIn_eq_inertiaDeg ğ’‘ (span {hÎ¶.toInteger - 1}) Gal(K/â„š),
    inertiaDeg_span_zeta_sub_one,
    ramificationIdx_span_zeta_sub_one, mul_one, â† Nat.totient_prime_pow_succ hp.out,
    â† finrank _ K, IsGaloisGroup.card_eq_finrank Gal(K/â„š) â„š K, Nat.mul_eq_right] at h_main
  exact Module.finrank_pos.ne'

theorem eq_span_zeta_sub_one_of_liesOver (P : Ideal (ğ“ K)) [hPâ‚ : P.IsPrime] [hPâ‚‚ : P.LiesOver ğ’‘] :
    P = span {hÎ¶.toInteger - 1} := by
  have : P âˆˆ primesOver ğ’‘ (ğ“ K) := âŸ¨hPâ‚, hPâ‚‚âŸ©
  have : span {hÎ¶.toInteger - 1} âˆˆ primesOver ğ’‘ (ğ“ K) :=
    âŸ¨isPrime_span_zeta_sub_one p k hÎ¶, liesOver_span_zeta_sub_one p k hÎ¶âŸ©
  have := ncard_primesOver_of_prime_pow p k K
  aesop

include hK in
theorem inertiaDeg_eq_of_prime_pow (P : Ideal (ğ“ K)) [hPâ‚ : P.IsPrime] [hPâ‚‚ : P.LiesOver ğ’‘] :
    inertiaDeg ğ’‘ P = 1 := by
  rw [eq_span_zeta_sub_one_of_liesOver p k K hK.zeta_spec P, inertiaDeg_span_zeta_sub_one]

include hK in
theorem ramificationIdx_eq_of_prime_pow (P : Ideal (ğ“ K)) [hPâ‚ : P.IsPrime] [hPâ‚‚ : P.LiesOver ğ’‘] :
    ramificationIdx (algebraMap â„¤ (ğ“ K)) ğ’‘ P = p ^ k * (p - 1) := by
  rw [eq_span_zeta_sub_one_of_liesOver p k K hK.zeta_spec P, ramificationIdx_span_zeta_sub_one]

include hK in
theorem inertiaDegIn_eq_of_prime_pow :
    ğ’‘.inertiaDegIn (ğ“ K) = 1 := by
  have : IsGalois â„š K := isGalois {p ^ (k + 1)} â„š K
  rw [inertiaDegIn_eq_inertiaDeg ğ’‘ (span {hK.zeta_spec.toInteger - 1}) Gal(K/â„š),
    inertiaDeg_span_zeta_sub_one]

include hK in
theorem ramificationIdxIn_eq_of_prime_pow :
    ğ’‘.ramificationIdxIn (ğ“ K) = p ^ k * (p - 1) := by
  have : IsGalois â„š K := isGalois {p ^ (k + 1)} â„š K
  rw [ramificationIdxIn_eq_ramificationIdx ğ’‘ (span {hK.zeta_spec.toInteger - 1}) Gal(K/â„š),
    ramificationIdx_span_zeta_sub_one]

end PrimePow

section Prime

variable {K} [hK : IsCyclotomicExtension {p} â„š K] {Î¶ : K} (hÎ¶ : IsPrimitiveRoot Î¶ p)

instance isPrime_span_zeta_sub_one' : IsPrime (span {hÎ¶.toInteger - 1}) := by
  rw [â† pow_one p] at hK hÎ¶
  exact isPrime_span_zeta_sub_one p 0 hÎ¶

theorem inertiaDeg_span_zeta_sub_one' : inertiaDeg ğ’‘ (span {hÎ¶.toInteger - 1}) = 1 := by
  rw [â† pow_one p] at hK hÎ¶
  exact inertiaDeg_span_zeta_sub_one p 0 hÎ¶

theorem ramificationIdx_span_zeta_sub_one' :
    ramificationIdx (algebraMap â„¤ (ğ“ K)) ğ’‘ (span {hÎ¶.toInteger - 1}) = p - 1 := by
  rw [â† pow_one p] at hK hÎ¶
  rw [ramificationIdx_span_zeta_sub_one p 0 hÎ¶, pow_zero, one_mul]

variable (K)

include hK in
theorem ncard_primesOver_of_prime :
    (primesOver ğ’‘ (ğ“ K)).ncard = 1 := by
  rw [â† pow_one p] at hK
  exact ncard_primesOver_of_prime_pow p 0 K

theorem eq_span_zeta_sub_one_of_liesOver' (P : Ideal (ğ“ K)) [hPâ‚ : P.IsPrime] [hPâ‚‚ : P.LiesOver ğ’‘] :
    P = span {hÎ¶.toInteger - 1} := by
  rw [â† pow_one p] at hK hÎ¶
  exact eq_span_zeta_sub_one_of_liesOver p 0 K hÎ¶ P

include hK in
theorem inertiaDeg_eq_of_prime (P : Ideal (ğ“ K)) [hPâ‚ : P.IsPrime] [hPâ‚‚ : P.LiesOver ğ’‘] :
    inertiaDeg ğ’‘ P = 1 := by
  rw [eq_span_zeta_sub_one_of_liesOver' p K hK.zeta_spec P, inertiaDeg_span_zeta_sub_one']

include hK in
theorem ramificationIdx_eq_of_prime (P : Ideal (ğ“ K)) [hPâ‚ : P.IsPrime] [hPâ‚‚ : P.LiesOver ğ’‘] :
    ramificationIdx (algebraMap â„¤ (ğ“ K)) ğ’‘ P = p - 1 := by
  rw [eq_span_zeta_sub_one_of_liesOver' p K hK.zeta_spec P, ramificationIdx_span_zeta_sub_one']

include hK in
theorem inertiaDegIn_eq_of_prime :
    ğ’‘.inertiaDegIn (ğ“ K) = 1 := by
  rw [â† pow_one p] at hK
  exact inertiaDegIn_eq_of_prime_pow p 0 K

include hK in
theorem ramificationIdxIn_eq_of_prime :
    ğ’‘.ramificationIdxIn (ğ“ K) = p - 1 := by
  rw [â† pow_one p] at hK
  rw [ramificationIdxIn_eq_of_prime_pow p 0, pow_zero, one_mul]

@[deprecated (since := "2025-12-03")] alias ramificationIdxIn_of_prime :=
  ramificationIdxIn_eq_of_prime

end Prime

section notDvd

open NumberField.Ideal Polynomial

variable {m} [NeZero m] [hK : IsCyclotomicExtension {m} â„š K]

theorem inertiaDeg_eq_of_not_dvd (hm : Â¬ p âˆ£ m) :
    inertiaDeg ğ’‘ P = orderOf (p : ZMod m) := by
  replace hm : p.Coprime m := hp.out.coprime_iff_not_dvd.mpr hm
  let Î¶ := (zeta_spec m â„š K).toInteger
  have hâ‚ : Â¬ p âˆ£ exponent Î¶ := by
    rw [exponent_eq_one_iff.mpr <| adjoin_singleton_eq_top (zeta_spec m â„š K)]
    exact hp.out.not_dvd_one
  have hâ‚‚ := (primesOverSpanEquivMonicFactorsMod hâ‚ âŸ¨P, âŸ¨inferInstance, inferInstanceâŸ©âŸ©).2
  have hâ‚ƒ := inertiaDeg_primesOverSpanEquivMonicFactorsMod_symm_apply' hâ‚ hâ‚‚
  simp only [Subtype.coe_eta, Equiv.symm_apply_apply] at hâ‚ƒ
  rw [Multiset.mem_toFinset, Polynomial.mem_normalizedFactors_iff
    (map_monic_ne_zero (minpoly.monic Î¶.isIntegral))] at hâ‚‚
  rw [hâ‚ƒ, natDegree_of_dvd_cyclotomic_of_irreducible (by simp) hm (f := 1) _ hâ‚‚.1]
  Â· simpa using (orderOf_injective _ Units.coeHom_injective (ZMod.unitOfCoprime p hm)).symm
  Â· refine dvd_trans hâ‚‚.2.2 ?_
    rw [â† map_cyclotomic_int, cyclotomic_eq_minpoly (zeta_spec m â„š K) (NeZero.pos _),
      â† (zeta_spec m â„š K).coe_toInteger, â† RingOfIntegers.minpoly_coe Î¶]
    simp [Î¶]

@[deprecated (since := "2025-12-10")]
alias inertiaDeg_of_not_dvd := inertiaDeg_eq_of_not_dvd

theorem ramificationIdx_eq_of_not_dvd (hm : Â¬ p âˆ£ m) :
    ramificationIdx (algebraMap â„¤ (ğ“ K)) ğ’‘ P = 1 := by
  let Î¶ := (zeta_spec m â„š K).toInteger
  have hâ‚ : Â¬ p âˆ£ exponent Î¶ := by
    rw [exponent_eq_one_iff.mpr <| adjoin_singleton_eq_top (zeta_spec m â„š K)]
    exact hp.out.not_dvd_one
  have hâ‚‚ := (primesOverSpanEquivMonicFactorsMod hâ‚ âŸ¨P, âŸ¨inferInstance, inferInstanceâŸ©âŸ©).2
  have hâ‚ƒ := ramificationIdx_primesOverSpanEquivMonicFactorsMod_symm_apply' hâ‚ hâ‚‚
  simp only [Subtype.coe_eta, Equiv.symm_apply_apply] at hâ‚ƒ
  rw [Multiset.mem_toFinset, Polynomial.mem_normalizedFactors_iff
    (map_monic_ne_zero (minpoly.monic Î¶.isIntegral))] at hâ‚‚
  rw [hâ‚ƒ]
  refine multiplicity_eq_of_emultiplicity_eq_some (le_antisymm ?_ ?_)
  Â· apply emultiplicity_le_one_of_separable
    Â· exact isUnit_iff_degree_eq_zero.not.mpr (Irreducible.degree_pos hâ‚‚.1).ne'
    Â· exact (zeta_spec m â„š K).toInteger_isPrimitiveRoot.separable_minpoly_mod hm
  Â· rw [ENat.coe_one]
    exact Order.one_le_iff_pos.mpr <| emultiplicity_pos_of_dvd hâ‚‚.2.2

@[deprecated (since := "2025-12-10")]
alias ramificationIdx_of_not_dvd := ramificationIdx_eq_of_not_dvd

theorem inertiaDegIn_eq_of_not_dvd (hm : Â¬ p âˆ£ m) :
    ğ’‘.inertiaDegIn (ğ“ K) = orderOf (p : ZMod m) := by
  have : IsGalois â„š K := isGalois {m} â„š K
  obtain âŸ¨âŸ¨P, _, _âŸ©âŸ© := ğ’‘.nonempty_primesOver (S := ğ“ K)
  rw [inertiaDegIn_eq_inertiaDeg ğ’‘ P Gal(K/â„š), inertiaDeg_eq_of_not_dvd p K P hm]

theorem ramificationIdxIn_eq_of_not_dvd (hm : Â¬ p âˆ£ m) :
    ğ’‘.ramificationIdxIn (ğ“ K) = 1 := by
  have : IsGalois â„š K := isGalois {m} â„š K
  obtain âŸ¨âŸ¨P, _, _âŸ©âŸ© := ğ’‘.nonempty_primesOver (S := ğ“ K)
  rw [ramificationIdxIn_eq_ramificationIdx ğ’‘ P Gal(K/â„š), ramificationIdx_eq_of_not_dvd p K P hm]

@[deprecated (since := "2025-12-03")] alias inertiaDegIn_of_not_dvd := inertiaDegIn_eq_of_not_dvd
@[deprecated (since := "2025-12-03")] alias ramificationIdxIn_of_not_dvd :=
  ramificationIdxIn_eq_of_not_dvd

end notDvd

section general

variable {m p k} [IsCyclotomicExtension {n} â„š K]

open IntermediateField in
private theorem inertiaDegIn_ramificationIdxIn_aux (hn : n = p ^ (k + 1) * m) (hm : Â¬ p âˆ£ m) :
    ğ’‘.inertiaDegIn (ğ“ K) = orderOf (p : ZMod m) âˆ§
      ğ’‘.ramificationIdxIn (ğ“ K) = p ^ k * (p - 1) := by
  have : IsAbelianGalois â„š K := IsCyclotomicExtension.isAbelianGalois {n} â„š K
  have : NeZero m := âŸ¨fun h â†¦ by simp [h] at hmâŸ©
  have : NeZero n := âŸ¨hn â–¸ NeZero.ne (p ^ (k + 1) * m)âŸ©
  have hp' : ğ’‘ â‰  âŠ¥ := by simpa using hp.out.ne_zero
  let Î¶ := zeta n â„š K
  have hÎ¶ := zeta_spec n â„š K
  -- We construct `â„šâŸ®Î¶â‚˜âŸ¯ âŠ† â„šâŸ®Î¶â‚™âŸ¯`
  let Î¶â‚˜ := Î¶ ^ (p ^ (k + 1))
  have hÎ¶â‚˜ := hÎ¶.pow (NeZero.pos _) hn
  let Fâ‚˜ := â„šâŸ®Î¶â‚˜âŸ¯
  have : IsCyclotomicExtension {m} â„š Fâ‚˜ :=
    (isCyclotomicExtension_singleton_iff_eq_adjoin _ _ _ _ hÎ¶â‚˜).mpr rfl
  -- A prime ideal of `Fâ‚˜` above `ğ’‘`
  obtain âŸ¨Pâ‚˜, _, _âŸ© := exists_maximal_ideal_liesOver_of_isIntegral ğ’‘ (S := ğ“ Fâ‚˜)
  -- We construct `â„šâŸ®Î¶_p^{k+1}âŸ¯ âŠ† â„šâŸ®Î¶â‚˜âŸ¯`
  let Î¶â‚š := Î¶ ^ m
  have hÎ¶â‚š := hÎ¶.pow (NeZero.pos _) (mul_comm _ m â–¸ hn)
  let Fâ‚š := â„šâŸ®Î¶â‚šâŸ¯
  have : IsCyclotomicExtension {p ^ (k + 1)} â„š Fâ‚š :=
    (isCyclotomicExtension_singleton_iff_eq_adjoin _ _ _ _ hÎ¶â‚š).mpr rfl
  -- A prime ideal of `Fâ‚š` above `ğ’‘`
  obtain âŸ¨Pâ‚š, hPâ‚, _âŸ© := exists_maximal_ideal_liesOver_of_isIntegral ğ’‘ (S := ğ“ Fâ‚š)
  have hPp : Ideal.map (algebraMap (ğ“ Fâ‚š) (ğ“ K)) Pâ‚š â‰  âŠ¥ :=
    map_ne_bot_of_ne_bot <| Ring.ne_bot_of_isMaximal_of_not_isField hPâ‚ <| not_isField Fâ‚š
  suffices Pâ‚š.ramificationIdxIn (ğ“ K) *
      Pâ‚˜.inertiaDegIn (ğ“ K) * (Pâ‚˜.primesOver (ğ“ K)).ncard = 1 by
    replace this := Nat.eq_one_of_mul_eq_one_right this
    rw [â† inertiaDegIn_mul_inertiaDegIn ğ’‘ Pâ‚˜ Gal(Fâ‚˜/â„š) _ Gal(K/â„š) Gal(K/Fâ‚˜),
      â† ramificationIdxIn_mul_ramificationIdxIn Pâ‚š Gal(Fâ‚š/â„š) _ Gal(K/â„š) Gal(K/Fâ‚š)
      (map_ne_bot_of_ne_bot hp') hPp, Nat.eq_one_of_mul_eq_one_left this,
      Nat.eq_one_of_mul_eq_one_right this, mul_one, mul_one,
      inertiaDegIn_eq_of_not_dvd p _ hm, ramificationIdxIn_eq_of_prime_pow p k Fâ‚š]
    exact âŸ¨rfl, rflâŸ©
  have h_main : Module.finrank â„š Fâ‚˜ * Module.finrank â„š Fâ‚š = Module.finrank â„š K := by
    rw [finrank m, finrank (p ^ (k + 1)), finrank n, hn, mul_comm, Nat.totient_mul]
    exact Nat.Coprime.pow_left (k + 1) (by rwa [hp.out.coprime_iff_not_dvd])
  rwa [â† IsGalois.card_aut_eq_finrank, â† IsGalois.card_aut_eq_finrank,
    â† IsGalois.card_aut_eq_finrank,
    â† ncard_primesOver_mul_ramificationIdxIn_mul_inertiaDegIn hp' (ğ“ Fâ‚˜) Gal(Fâ‚˜/â„š),
    â† ncard_primesOver_mul_ramificationIdxIn_mul_inertiaDegIn hp' (ğ“ Fâ‚š) Gal(Fâ‚š/â„š),
    â† ncard_primesOver_mul_ramificationIdxIn_mul_inertiaDegIn hp' (ğ“ K) Gal(K/â„š),
    â† ncard_primesOver_mul_ncard_primesOver Pâ‚˜ Gal(Fâ‚˜/â„š) (ğ“ K) Gal(K/â„š) Gal(K/Fâ‚˜) hp',
    ramificationIdxIn_eq_of_not_dvd p Fâ‚˜ hm, inertiaDegIn_eq_of_prime_pow p k Fâ‚š,
    ncard_primesOver_of_prime_pow p k Fâ‚š, one_mul, one_mul, mul_one, mul_assoc, mul_assoc,
    mul_right_inj' (primesOver_ncard_ne_zero ğ’‘ _), â† mul_assoc, â† mul_rotate (ğ’‘.inertiaDegIn (ğ“ K)),
    â† inertiaDegIn_mul_inertiaDegIn ğ’‘ Pâ‚˜ Gal(Fâ‚˜/â„š) (ğ“ K) Gal(K/â„š) Gal(K/Fâ‚˜), mul_assoc, mul_assoc,
    mul_right_inj' (inertiaDegIn_ne_zero Gal(Fâ‚˜/â„š)), â† mul_rotate',
    â† ramificationIdxIn_mul_ramificationIdxIn (p := ğ’‘) Pâ‚š Gal(Fâ‚š/â„š) (ğ“ K) Gal(K/â„š) Gal(K/Fâ‚š)
    (map_ne_bot_of_ne_bot hp') hPp, eq_comm, mul_assoc,
    mul_eq_leftâ‚€ (ramificationIdxIn_ne_zero Gal(Fâ‚š/â„š) hp'), â† mul_assoc] at h_main

/--
Write `n = p ^ (k + 1) * m` where the prime `p` does not divide `m`, then the inertia degree of
`p` in `â„š(Î¶â‚™)` is the order of `p` modulo `m`.
-/
theorem inertiaDegIn_eq (hn : n = p ^ (k + 1) * m) (hm : Â¬ p âˆ£ m) :
    ğ’‘.inertiaDegIn (ğ“ K) = orderOf (p : ZMod m) :=
  (inertiaDegIn_ramificationIdxIn_aux n K hn hm).1

/--
Write `n = p ^ (k + 1) * m` where the prime `p` does not divide `m`, then the ramification index
of `p` in `â„š(Î¶â‚™)` is `p ^ k * (p - 1)`.
-/
theorem ramificationIdxIn_eq (hn : n = p ^ (k + 1) * m) (hm : Â¬ p âˆ£ m) :
    ğ’‘.ramificationIdxIn (ğ“ K) = p ^ k * (p - 1) :=
  (inertiaDegIn_ramificationIdxIn_aux n K hn hm).2

theorem inertiaDeg_eq (hn : n = p ^ (k + 1) * m) (hm : Â¬ p âˆ£ m) :
    inertiaDeg ğ’‘ P = orderOf (p : ZMod m) := by
  have : IsGalois â„š K := isGalois {n} â„š K
  rw [â† inertiaDegIn_eq_inertiaDeg ğ’‘ P Gal(K/â„š), inertiaDegIn_eq n K hn hm]

theorem ramificationIdx_eq (hn : n = p ^ (k + 1) * m) (hm : Â¬ p âˆ£ m) :
    ramificationIdx (algebraMap â„¤ (ğ“ K)) ğ’‘ P = p ^ k * (p - 1) := by
  have : IsGalois â„š K := isGalois {n} â„š K
  rw [â† ramificationIdxIn_eq_ramificationIdx ğ’‘ P Gal(K/â„š), ramificationIdxIn_eq n K hn hm]

end general

end IsCyclotomicExtension.Rat
