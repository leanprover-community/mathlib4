/-
Copyright (c) 2023 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.LinearAlgebra.Matrix.Gershgorin
import Mathlib.NumberTheory.NumberField.CanonicalEmbedding.ConvexBody
import Mathlib.NumberTheory.NumberField.Units.Basic

/-!
# Dirichlet theorem on the group of units of a number field
This file is devoted to the proof of Dirichlet unit theorem that states that the group of
units `(ùìû K)À£` of units of the ring of integers `ùìû K` of a number field `K` modulo its torsion
subgroup is a free `‚Ñ§`-module of rank `card (InfinitePlace K) - 1`.

## Main definitions

* `NumberField.Units.rank`: the unit rank of the number field `K`.

* `NumberField.Units.fundSystem`: a fundamental system of units of `K`.

* `NumberField.Units.basisModTorsion`: a `‚Ñ§`-basis of `(ùìû K)À£ ‚ß∏ (torsion K)`
  as an additive `‚Ñ§`-module.

## Main results

* `NumberField.Units.rank_modTorsion`: the `‚Ñ§`-rank of `(ùìû K)À£ ‚ß∏ (torsion K)` is equal to
  `card (InfinitePlace K) - 1`.

* `NumberField.Units.exist_unique_eq_mul_prod`: **Dirichlet Unit Theorem**. Any unit of `ùìû K`
  can be written uniquely as the product of a root of unity and powers of the units of the
  fundamental system `fundSystem`.

## Tags
number field, units, Dirichlet unit theorem
-/

noncomputable section

open Module NumberField NumberField.InfinitePlace NumberField.Units

variable (K : Type*) [Field K]

namespace NumberField.Units.dirichletUnitTheorem

/-!
### Dirichlet Unit Theorem

We define a group morphism from `(ùìû K)À£` to `logSpace K`, defined as
`{w : InfinitePlace K // w ‚â† w‚ÇÄ} ‚Üí ‚Ñù` where `w‚ÇÄ` is a distinguished (arbitrary) infinite place,
prove that its kernel is the torsion subgroup (see `logEmbedding_eq_zero_iff`) and that its image,
called `unitLattice`, is a full `‚Ñ§`-lattice. It follows that `unitLattice` is a free `‚Ñ§`-module
(see `instModuleFree_unitLattice`) of rank `card (InfinitePlaces K) - 1` (see `unitLattice_rank`).
To prove that the `unitLattice` is a full `‚Ñ§`-lattice, we need to prove that it is discrete
(see `unitLattice_inter_ball_finite`) and that it spans the full space over `‚Ñù`
(see `unitLattice_span_eq_top`); this is the main part of the proof, see the section `span_top`
below for more details.
-/

open Finset

variable {K}

section NumberField

variable [NumberField K]

/-- The distinguished infinite place. -/
def w‚ÇÄ : InfinitePlace K := (inferInstance : Nonempty (InfinitePlace K)).some

variable (K) in
/-- The `logSpace` is defined as `{w : InfinitePlace K // w ‚â† w‚ÇÄ} ‚Üí ‚Ñù` where `w‚ÇÄ` is the
distinguished infinite place. -/
abbrev logSpace := {w : InfinitePlace K // w ‚â† w‚ÇÄ} ‚Üí ‚Ñù

variable (K) in
/-- The logarithmic embedding of the units (seen as an `Additive` group). -/
def _root_.NumberField.Units.logEmbedding :
    Additive ((ùìû K)À£) ‚Üí+ logSpace K :=
{ toFun := fun x w ‚Ü¶ mult w.val * Real.log (w.val ‚Üëx.toMul)
  map_zero' := by simp; rfl
  map_add' := fun _ _ ‚Ü¶ by simp [Real.log_mul, mul_add]; rfl }

@[simp]
theorem logEmbedding_component (x : (ùìû K)À£) (w : {w : InfinitePlace K // w ‚â† w‚ÇÄ}) :
    (logEmbedding K (Additive.ofMul x)) w = mult w.val * Real.log (w.val x) := rfl

open scoped Classical in
theorem sum_logEmbedding_component (x : (ùìû K)À£) :
    ‚àë w, logEmbedding K (Additive.ofMul x) w =
      -mult (w‚ÇÄ : InfinitePlace K) * Real.log (w‚ÇÄ (x : K)) := by
  have h := sum_mult_mul_log x
  rw [Fintype.sum_eq_add_sum_subtype_ne _ w‚ÇÄ, add_comm, add_eq_zero_iff_eq_neg, ‚Üê neg_mul] at h
  simpa [logEmbedding_component] using h

end NumberField

theorem mult_log_place_eq_zero {x : (ùìû K)À£} {w : InfinitePlace K} :
    mult w * Real.log (w x) = 0 ‚Üî w x = 1 := by
  rw [mul_eq_zero, or_iff_right, Real.log_eq_zero, or_iff_right, or_iff_left]
  ¬∑ linarith [(apply_nonneg _ _ : 0 ‚â§ w x)]
  ¬∑ exact (Units.pos_at_place _ _).ne'
  ¬∑ exact mult_coe_ne_zero

variable [NumberField K]

theorem logEmbedding_eq_zero_iff {x : (ùìû K)À£} :
    logEmbedding K (Additive.ofMul x) = 0 ‚Üî x ‚àà torsion K := by
  rw [mem_torsion]
  refine ‚ü®fun h w ‚Ü¶ ?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ by_cases hw : w = w‚ÇÄ
    ¬∑ suffices -mult w‚ÇÄ * Real.log (w‚ÇÄ (x : K)) = 0 by
        rw [neg_mul, neg_eq_zero, ‚Üê hw] at this
        exact mult_log_place_eq_zero.mp this
      rw [‚Üê sum_logEmbedding_component, sum_eq_zero]
      exact fun w _ ‚Ü¶ congrFun h w
    ¬∑ exact mult_log_place_eq_zero.mp (congrFun h ‚ü®w, hw‚ü©)
  ¬∑ ext w
    rw [logEmbedding_component, h w.val, Real.log_one, mul_zero, Pi.zero_apply]

open scoped Classical in
theorem logEmbedding_component_le {r : ‚Ñù} {x : (ùìû K)À£} (hr : 0 ‚â§ r) (h : ‚ÄñlogEmbedding K x‚Äñ ‚â§ r)
    (w : {w : InfinitePlace K // w ‚â† w‚ÇÄ}) : |logEmbedding K (Additive.ofMul x) w| ‚â§ r := by
  lift r to NNReal using hr
  simp_rw [Pi.norm_def, NNReal.coe_le_coe, Finset.sup_le_iff, ‚Üê NNReal.coe_le_coe] at h
  exact h w (mem_univ _)

open scoped Classical in
theorem log_le_of_logEmbedding_le {r : ‚Ñù} {x : (ùìû K)À£} (hr : 0 ‚â§ r)
    (h : ‚ÄñlogEmbedding K (Additive.ofMul x)‚Äñ ‚â§ r) (w : InfinitePlace K) :
    |Real.log (w x)| ‚â§ (Fintype.card (InfinitePlace K)) * r := by
  have tool : ‚àÄ x : ‚Ñù, 0 ‚â§ x ‚Üí x ‚â§ mult w * x := fun x hx ‚Ü¶ by
    nth_rw 1 [‚Üê one_mul x]
    refine mul_le_mul ?_ le_rfl hx ?_
    all_goals { rw [mult]; split_ifs <;> norm_num }
  by_cases hw : w = w‚ÇÄ
  ¬∑ have hyp := congr_arg (‚Äñ¬∑‚Äñ) (sum_logEmbedding_component x).symm
    replace hyp := (le_of_eq hyp).trans (norm_sum_le _ _)
    simp_rw [norm_mul, norm_neg, Real.norm_eq_abs, Nat.abs_cast] at hyp
    refine (le_trans ?_ hyp).trans ?_
    ¬∑ rw [‚Üê hw]
      exact tool _ (abs_nonneg _)
    ¬∑ refine (sum_le_card_nsmul univ _ _
        (fun w _ ‚Ü¶ logEmbedding_component_le hr h w)).trans ?_
      rw [nsmul_eq_mul]
      refine mul_le_mul ?_ le_rfl hr (Fintype.card (InfinitePlace K)).cast_nonneg
      simp
  ¬∑ have hyp := logEmbedding_component_le hr h ‚ü®w, hw‚ü©
    rw [logEmbedding_component, abs_mul, Nat.abs_cast] at hyp
    refine (le_trans ?_ hyp).trans ?_
    ¬∑ exact tool _ (abs_nonneg _)
    ¬∑ nth_rw 1 [‚Üê one_mul r]
      exact mul_le_mul (Nat.one_le_cast.mpr Fintype.card_pos) (le_of_eq rfl) hr (Nat.cast_nonneg _)

variable (K)

/-- The lattice formed by the image of the logarithmic embedding. -/
noncomputable def _root_.NumberField.Units.unitLattice :
    Submodule ‚Ñ§ (logSpace K) :=
  Submodule.map (logEmbedding K).toIntLinearMap ‚ä§

open scoped Classical in
theorem unitLattice_inter_ball_finite (r : ‚Ñù) :
    ((unitLattice K : Set (logSpace K)) ‚à© Metric.closedBall 0 r).Finite := by
  obtain hr | hr := lt_or_ge r 0
  ¬∑ convert Set.finite_empty
    rw [Metric.closedBall_eq_empty.mpr hr]
    exact Set.inter_empty _
  ¬∑ suffices {x : (ùìû K)À£ | IsIntegral ‚Ñ§ (x : K) ‚àß
        ‚àÄ (œÜ : K ‚Üí+* ‚ÑÇ), ‚ÄñœÜ x‚Äñ ‚â§ Real.exp ((Fintype.card (InfinitePlace K)) * r)}.Finite by
      refine (Set.Finite.image (logEmbedding K) this).subset ?_
      rintro _ ‚ü®‚ü®x, ‚ü®_, rfl‚ü©‚ü©, hx‚ü©
      refine ‚ü®x, ‚ü®x.val.prop, (le_iff_le _ _).mp (fun w ‚Ü¶ (Real.log_le_iff_le_exp ?_).mp ?_)‚ü©, rfl‚ü©
      ¬∑ exact pos_iff.mpr (coe_ne_zero x)
      ¬∑ rw [mem_closedBall_zero_iff] at hx
        exact (le_abs_self _).trans (log_le_of_logEmbedding_le hr hx w)
    refine Set.Finite.of_finite_image ?_ (coe_injective K).injOn
    refine (Embeddings.finite_of_norm_le K ‚ÑÇ
        (Real.exp ((Fintype.card (InfinitePlace K)) * r))).subset ?_
    rintro _ ‚ü®x, ‚ü®‚ü®h_int, h_le‚ü©, rfl‚ü©‚ü©
    exact ‚ü®h_int, h_le‚ü©

section span_top

/-!
#### Section `span_top`

In this section, we prove that the span over `‚Ñù` of the `unitLattice` is equal to the full space.
For this, we construct for each infinite place `w‚ÇÅ ‚â† w‚ÇÄ` a unit `u_w‚ÇÅ` of `K` such that, for all
infinite places `w` such that `w ‚â† w‚ÇÅ`, we have `Real.log w (u_w‚ÇÅ) < 0`
(and thus `Real.log w‚ÇÅ (u_w‚ÇÅ) > 0`). It follows then from a determinant computation
(using `Matrix.det_ne_zero_of_sum_col_lt_diag`) that the image by `logEmbedding` of these units is
a `‚Ñù`-linearly independent family. The unit `u_w‚ÇÅ` is obtained by constructing a sequence `seq n`
of nonzero algebraic integers that is strictly decreasing at infinite places distinct from `w‚ÇÅ` and
of norm `‚â§ B`. Since there are finitely many ideals of norm `‚â§ B`, there exists two term in the
sequence defining the same ideal and their quotient is the desired unit `u_w‚ÇÅ` (see `exists_unit`).
-/

open NumberField.mixedEmbedding NNReal

variable (w‚ÇÅ : InfinitePlace K) {B : ‚Ñï} (hB : minkowskiBound K 1 < (convexBodyLTFactor K) * B)

include hB in
/-- This result shows that there always exists a next term in the sequence. -/
theorem seq_next {x : ùìû K} (hx : x ‚â† 0) :
    ‚àÉ y : ùìû K, y ‚â† 0 ‚àß
      (‚àÄ w, w ‚â† w‚ÇÅ ‚Üí w y < w x) ‚àß
      |Algebra.norm ‚Ñö (y : K)| ‚â§ B := by
  have hx' := RingOfIntegers.coe_ne_zero_iff.mpr hx
  let f : InfinitePlace K ‚Üí ‚Ñù‚â•0 :=
    fun w ‚Ü¶ ‚ü®(w x) / 2, div_nonneg (AbsoluteValue.nonneg _ _) (by norm_num)‚ü©
  suffices ‚àÄ w, w ‚â† w‚ÇÅ ‚Üí f w ‚â† 0 by
    obtain ‚ü®g, h_geqf, h_gprod‚ü© := adjust_f K B this
    obtain ‚ü®y, h_ynz, h_yle‚ü© := exists_ne_zero_mem_ringOfIntegers_lt K (f := g)
      (by rw [convexBodyLT_volume]; convert hB; exact congr_arg ((‚Üë) : NNReal ‚Üí ENNReal) h_gprod)
    refine ‚ü®y, h_ynz, fun w hw ‚Ü¶ (h_geqf w hw ‚ñ∏ h_yle w).trans ?_, ?_‚ü©
    ¬∑ rw [‚Üê Rat.cast_le (K := ‚Ñù), Rat.cast_natCast]
      calc
        _ = ‚àè w : InfinitePlace K, w (algebraMap _ K y) ^ mult w :=
          (prod_eq_abs_norm (algebraMap _ K y)).symm
        _ ‚â§ ‚àè w : InfinitePlace K, (g w : ‚Ñù) ^ mult w := by gcongr with w; exact (h_yle w).le
        _ ‚â§ (B : ‚Ñù) := by
          simp_rw [‚Üê NNReal.coe_pow, ‚Üê NNReal.coe_prod]
          exact le_of_eq (congr_arg toReal h_gprod)
    ¬∑ refine div_lt_self ?_ (by norm_num)
      exact pos_iff.mpr hx'
  intro _ _
  rw [ne_eq, Nonneg.mk_eq_zero, div_eq_zero_iff, map_eq_zero, not_or]
  exact ‚ü®hx', by norm_num‚ü©

/-- An infinite sequence of nonzero algebraic integers of `K` satisfying the following properties:
‚Ä¢ `seq n` is nonzero;
‚Ä¢ for `w : InfinitePlace K`, `w ‚â† w‚ÇÅ ‚Üí w (seq n + 1) < w (seq n)`;
‚Ä¢ `‚à£norm (seq n)‚à£ ‚â§ B`. -/
def seq : ‚Ñï ‚Üí { x : ùìû K // x ‚â† 0 }
  | 0 => ‚ü®1, by norm_num‚ü©
  | n + 1 =>
    ‚ü®(seq_next K w‚ÇÅ hB (seq n).prop).choose, (seq_next K w‚ÇÅ hB (seq n).prop).choose_spec.1‚ü©

/-- The terms of the sequence are nonzero. -/
theorem seq_ne_zero (n : ‚Ñï) : algebraMap (ùìû K) K (seq K w‚ÇÅ hB n) ‚â† 0 :=
  RingOfIntegers.coe_ne_zero_iff.mpr (seq K w‚ÇÅ hB n).prop

/-- The sequence is strictly decreasing at infinite places distinct from `w‚ÇÅ`. -/
theorem seq_decreasing {n m : ‚Ñï} (h : n < m) (w : InfinitePlace K) (hw : w ‚â† w‚ÇÅ) :
    w (algebraMap (ùìû K) K (seq K w‚ÇÅ hB m)) < w (algebraMap (ùìû K) K (seq K w‚ÇÅ hB n)) := by
  induction m with
  | zero =>
      exfalso
      exact Nat.not_succ_le_zero n h
  | succ m m_ih =>
      cases eq_or_lt_of_le (Nat.le_of_lt_succ h) with
      | inl hr =>
          rw [hr]
          exact (seq_next K w‚ÇÅ hB (seq K w‚ÇÅ hB m).prop).choose_spec.2.1 w hw
      | inr hr =>
          refine lt_trans ?_ (m_ih hr)
          exact (seq_next K w‚ÇÅ hB (seq K w‚ÇÅ hB m).prop).choose_spec.2.1 w hw

/-- The terms of the sequence have norm bounded by `B`. -/
theorem seq_norm_le (n : ‚Ñï) :
    Int.natAbs (Algebra.norm ‚Ñ§ (seq K w‚ÇÅ hB n : ùìû K)) ‚â§ B := by
  cases n with
  | zero =>
      have : 1 ‚â§ B := by
        contrapose! hB
        simp only [Nat.lt_one_iff.mp hB, CharP.cast_eq_zero, mul_zero, zero_le]
      simp only [ne_eq, seq, map_one, Int.natAbs_one, this]
  | succ n =>
      rw [‚Üê Nat.cast_le (Œ± := ‚Ñö), Int.cast_natAbs, Int.cast_abs, Algebra.coe_norm_int]
      exact (seq_next K w‚ÇÅ hB (seq K w‚ÇÅ hB n).prop).choose_spec.2.2

/-- Construct a unit associated to the place `w‚ÇÅ`. The family, for `w‚ÇÅ ‚â† w‚ÇÄ`, formed by the
image by the `logEmbedding` of these units is `‚Ñù`-linearly independent, see
`unitLattice_span_eq_top`. -/
theorem exists_unit (w‚ÇÅ : InfinitePlace K) :
    ‚àÉ u : (ùìû K)À£, ‚àÄ w : InfinitePlace K, w ‚â† w‚ÇÅ ‚Üí Real.log (w u) < 0 := by
  obtain ‚ü®B, hB‚ü© : ‚àÉ B : ‚Ñï, minkowskiBound K 1 < (convexBodyLTFactor K) * B := by
    conv => congr; ext; rw [mul_comm]
    exact ENNReal.exists_nat_mul_gt (ENNReal.coe_ne_zero.mpr (convexBodyLTFactor_ne_zero K))
      (ne_of_lt (minkowskiBound_lt_top K 1))
  rsuffices ‚ü®n, m, hnm, h‚ü© : ‚àÉ n m, n < m ‚àß
      (Ideal.span ({ (seq K w‚ÇÅ hB n : ùìû K) }) = Ideal.span ({ (seq K w‚ÇÅ hB m : ùìû K) }))
  ¬∑ have hu := Ideal.span_singleton_eq_span_singleton.mp h
    refine ‚ü®hu.choose, fun w hw ‚Ü¶ Real.log_neg (pos_at_place hu.choose w) ?_‚ü©
    calc
      _ = w (algebraMap (ùìû K) K (seq K w‚ÇÅ hB m) * (algebraMap (ùìû K) K (seq K w‚ÇÅ hB n))‚Åª¬π) := by
        rw [‚Üê congr_arg (algebraMap (ùìû K) K) hu.choose_spec, mul_comm, map_mul (algebraMap _ _),
          ‚Üê mul_assoc, inv_mul_cancel‚ÇÄ (seq_ne_zero K w‚ÇÅ hB n), one_mul]
      _ = w (algebraMap (ùìû K) K (seq K w‚ÇÅ hB m)) * w (algebraMap (ùìû K) K (seq K w‚ÇÅ hB n))‚Åª¬π :=
        map_mul _ _ _
      _ < 1 := by
        rw [map_inv‚ÇÄ, mul_inv_lt_iff‚ÇÄ' (pos_iff.mpr (seq_ne_zero K w‚ÇÅ hB n)), mul_one]
        exact seq_decreasing K w‚ÇÅ hB hnm w hw
  refine Set.Finite.exists_lt_map_eq_of_forall_mem (t := {I : Ideal (ùìû K) | Ideal.absNorm I ‚â§ B})
    (fun n ‚Ü¶ ?_) (Ideal.finite_setOf_absNorm_le B)
  rw [Set.mem_setOf_eq, Ideal.absNorm_span_singleton]
  exact seq_norm_le K w‚ÇÅ hB n

theorem unitLattice_span_eq_top :
    Submodule.span ‚Ñù (unitLattice K : Set (logSpace K)) = ‚ä§ := by
  classical
  refine le_antisymm le_top ?_
  -- The standard basis
  let B := Pi.basisFun ‚Ñù {w : InfinitePlace K // w ‚â† w‚ÇÄ}
  -- The image by log_embedding of the family of units constructed above
  let v := fun w : { w : InfinitePlace K // w ‚â† w‚ÇÄ } ‚Ü¶
    logEmbedding K (Additive.ofMul (exists_unit K w).choose)
  -- To prove the result, it is enough to prove that the family `v` is linearly independent
  suffices B.det v ‚â† 0 by
    rw [‚Üê isUnit_iff_ne_zero, ‚Üê Basis.is_basis_iff_det] at this
    rw [‚Üê this.2]
    refine Submodule.span_monotone fun _ ‚ü®w, hw‚ü© ‚Ü¶ ‚ü®(exists_unit K w).choose, trivial, hw‚ü©
  rw [Basis.det_apply]
  -- We use a specific lemma to prove that this determinant is nonzero
  refine det_ne_zero_of_sum_col_lt_diag (fun w ‚Ü¶ ?_)
  simp_rw [Real.norm_eq_abs, B, Basis.coePiBasisFun.toMatrix_eq_transpose, Matrix.transpose_apply]
  rw [‚Üê sub_pos, sum_congr rfl (fun x hx ‚Ü¶ abs_of_neg ?_), sum_neg_distrib, sub_neg_eq_add,
    sum_erase_eq_sub (mem_univ _), ‚Üê add_comm_sub]
  ¬∑ refine add_pos_of_nonneg_of_pos ?_ ?_
    ¬∑ rw [sub_nonneg]
      exact le_abs_self _
    ¬∑ rw [sum_logEmbedding_component (exists_unit K w).choose]
      refine mul_pos_of_neg_of_neg ?_ ((exists_unit K w).choose_spec _ w.prop.symm)
      rw [mult]; split_ifs <;> norm_num
  ¬∑ refine mul_neg_of_pos_of_neg ?_ ((exists_unit K w).choose_spec x ?_)
    ¬∑ rw [mult]; split_ifs <;> norm_num
    ¬∑ exact Subtype.ext_iff_val.not.mp (ne_of_mem_erase hx)

end span_top

end dirichletUnitTheorem

section statements

variable [NumberField K]

open dirichletUnitTheorem Module

/-- The unit rank of the number field `K`, it is equal to `card (InfinitePlace K) - 1`. -/
def rank : ‚Ñï := Fintype.card (InfinitePlace K) - 1

instance instDiscrete_unitLattice : DiscreteTopology (unitLattice K) := by
  classical
  refine discreteTopology_of_isOpen_singleton_zero ?_
  refine isOpen_singleton_of_finite_mem_nhds 0 (s := Metric.closedBall 0 1) ?_ ?_
  ¬∑ exact Metric.closedBall_mem_nhds _ (by norm_num)
  ¬∑ refine Set.Finite.of_finite_image ?_ (Set.injOn_of_injective Subtype.val_injective)
    convert unitLattice_inter_ball_finite K 1
    ext x
    refine ‚ü®?_, fun ‚ü®hx1, hx2‚ü© ‚Ü¶ ‚ü®‚ü®x, hx1‚ü©, hx2, rfl‚ü©‚ü©
    rintro ‚ü®x, hx, rfl‚ü©
    exact ‚ü®Subtype.mem x, hx‚ü©

open scoped Classical in
instance instZLattice_unitLattice : IsZLattice ‚Ñù (unitLattice K) where
  span_top := unitLattice_span_eq_top K

protected theorem finrank_eq_rank :
    finrank ‚Ñù (logSpace K) = Units.rank K := by
  classical
  simp only [finrank_fintype_fun_eq_card, Fintype.card_subtype_compl,
    Fintype.card_ofSubsingleton, rank]

@[simp]
theorem unitLattice_rank :
    finrank ‚Ñ§ (unitLattice K) = Units.rank K := by
  classical
  rw [‚Üê Units.finrank_eq_rank, ZLattice.rank ‚Ñù]

/-- The map obtained by quotienting by the kernel of `logEmbedding`. -/
def logEmbeddingQuot :
    Additive ((ùìû K)À£ ‚ß∏ (torsion K)) ‚Üí+ logSpace K :=
  MonoidHom.toAdditive' <|
    (QuotientGroup.kerLift (AddMonoidHom.toMultiplicative' (logEmbedding K))).comp
      (QuotientGroup.quotientMulEquivOfEq (by
        ext
        rw [MonoidHom.mem_ker, AddMonoidHom.toMultiplicative'_apply_apply, ofAdd_eq_one,
          ‚Üê logEmbedding_eq_zero_iff])).toMonoidHom

@[simp]
theorem logEmbeddingQuot_apply (x : (ùìû K)À£) :
    logEmbeddingQuot K (Additive.ofMul (QuotientGroup.mk x)) =
      logEmbedding K (Additive.ofMul x) := rfl

theorem logEmbeddingQuot_injective :
    Function.Injective (logEmbeddingQuot K) := by
  unfold logEmbeddingQuot
  intro _ _ h
  simp_rw [MonoidHom.toAdditive'_apply_apply, MonoidHom.coe_comp, MulEquiv.coe_toMonoidHom,
    Function.comp_apply, EmbeddingLike.apply_eq_iff_eq] at h
  exact (EmbeddingLike.apply_eq_iff_eq _).mp <| (QuotientGroup.kerLift_injective _).eq_iff.mp h

/-- The linear equivalence between `(ùìû K)À£ ‚ß∏ (torsion K)` as an additive `‚Ñ§`-module and
`unitLattice` . -/
def logEmbeddingEquiv :
    Additive ((ùìû K)À£ ‚ß∏ (torsion K)) ‚âÉ‚Çó[‚Ñ§] (unitLattice K) :=
  LinearEquiv.ofBijective ((logEmbeddingQuot K).codRestrict (unitLattice K)
    (Quotient.ind fun _ ‚Ü¶ logEmbeddingQuot_apply K _ ‚ñ∏
      Submodule.mem_map_of_mem trivial)).toIntLinearMap
    ‚ü®fun _ _ ‚Ü¶ by
      rw [AddMonoidHom.coe_toIntLinearMap, AddMonoidHom.codRestrict_apply,
        AddMonoidHom.codRestrict_apply, Subtype.mk.injEq]
      apply logEmbeddingQuot_injective K, fun ‚ü®a, ‚ü®b, _, ha‚ü©‚ü© ‚Ü¶ ‚ü®‚ü¶b‚üß, by simpa using ha‚ü©‚ü©

@[simp]
theorem logEmbeddingEquiv_apply (x : (ùìû K)À£) :
    logEmbeddingEquiv K (Additive.ofMul (QuotientGroup.mk x)) =
      logEmbedding K (Additive.ofMul x) := rfl

instance : Module.Free ‚Ñ§ (Additive ((ùìû K)À£ ‚ß∏ (torsion K))) := by
  classical exact Module.Free.of_equiv (logEmbeddingEquiv K).symm

instance : Module.Finite ‚Ñ§ (Additive ((ùìû K)À£ ‚ß∏ (torsion K))) := by
  classical exact Module.Finite.equiv (logEmbeddingEquiv K).symm

-- Note that we prove this instance first and then deduce from it the instance
-- `Monoid.FG (ùìû K)À£`, and not the other way around, due to no `Subgroup` version
-- of `Submodule.fg_of_fg_map_of_fg_inf_ker` existing.
instance : Module.Finite ‚Ñ§ (Additive (ùìû K)À£) := by
  rw [Module.finite_def]
  refine Submodule.fg_of_fg_map_of_fg_inf_ker
    (MonoidHom.toAdditive (QuotientGroup.mk' (torsion K))).toIntLinearMap ?_ ?_
  ¬∑ rw [Submodule.map_top, LinearMap.range_eq_top.mpr
      (by exact QuotientGroup.mk'_surjective (torsion K)), ‚Üê Module.finite_def]
    infer_instance
  ¬∑ rw [inf_of_le_right le_top, AddMonoidHom.coe_toIntLinearMap_ker, MonoidHom.coe_toAdditive_ker,
      QuotientGroup.ker_mk', Submodule.fg_iff_add_subgroup_fg,
      AddSubgroup.toIntSubmodule_toAddSubgroup, ‚Üê AddGroup.fg_iff_addSubgroup_fg]
    have : Finite (Subgroup.toAddSubgroup (torsion K)) := (inferInstance : Finite (torsion K))
    exact AddGroup.fg_of_finite

instance : Monoid.FG (ùìû K)À£ := by
  rw [Monoid.fg_iff_add_fg, ‚Üê AddGroup.fg_iff_addMonoid_fg, ‚Üê Module.Finite.iff_addGroup_fg]
  infer_instance

theorem rank_modTorsion :
    Module.finrank ‚Ñ§ (Additive ((ùìû K)À£ ‚ß∏ (torsion K))) = rank K := by
  rw [‚Üê LinearEquiv.finrank_eq (logEmbeddingEquiv K).symm, unitLattice_rank]

/-- A basis of the quotient `(ùìû K)À£ ‚ß∏ (torsion K)` seen as an additive ‚Ñ§-module. -/
def basisModTorsion : Basis (Fin (rank K)) ‚Ñ§ (Additive ((ùìû K)À£ ‚ß∏ (torsion K))) :=
  Basis.reindex (Module.Free.chooseBasis ‚Ñ§ _) (Fintype.equivOfCardEq <| by
    rw [‚Üê Module.finrank_eq_card_chooseBasisIndex, rank_modTorsion, Fintype.card_fin])

/-- The basis of the `unitLattice` obtained by mapping `basisModTorsion` via `logEmbedding`. -/
def basisUnitLattice : Basis (Fin (rank K)) ‚Ñ§ (unitLattice K) :=
  (basisModTorsion K).map (logEmbeddingEquiv K)

/-- A fundamental system of units of `K`. The units of `fundSystem` are arbitrary lifts of the
units in `basisModTorsion`. -/
def fundSystem : Fin (rank K) ‚Üí (ùìû K)À£ :=
  -- `:)` prevents the `‚ß∏` decaying to a quotient by `leftRel` when we unfold this later
  fun i ‚Ü¶ Quotient.out ((basisModTorsion K i).toMul :)

theorem fundSystem_mk (i : Fin (rank K)) :
    Additive.ofMul (QuotientGroup.mk (fundSystem K i)) = (basisModTorsion K i) := by
  simp_rw [fundSystem, Equiv.apply_eq_iff_eq_symm_apply, Additive.ofMul_symm_eq, Quotient.out_eq']

theorem logEmbedding_fundSystem (i : Fin (rank K)) :
    logEmbedding K (Additive.ofMul (fundSystem K i)) = basisUnitLattice K i := by
  rw [basisUnitLattice, Basis.map_apply, ‚Üê fundSystem_mk, logEmbeddingEquiv_apply]

/-- The exponents that appear in the unique decomposition of a unit as the product of
a root of unity and powers of the units of the fundamental system `fundSystem` (see
`exist_unique_eq_mul_prod`) are given by the representation of the unit on `basisModTorsion`. -/
theorem fun_eq_repr {x Œ∂ : (ùìû K)À£} {f : Fin (rank K) ‚Üí ‚Ñ§} (hŒ∂ : Œ∂ ‚àà torsion K)
    (h : x = Œ∂ * ‚àè i, (fundSystem K i) ^ (f i)) :
    f = (basisModTorsion K).repr (Additive.ofMul ‚Üëx) := by
  suffices Additive.ofMul ‚Üëx = ‚àë i, (f i) ‚Ä¢ (basisModTorsion K i) by
    rw [‚Üê (basisModTorsion K).repr_sum_self f, ‚Üê this]
  calc
    Additive.ofMul ‚Üëx
    _ = ‚àë i, (f i) ‚Ä¢ Additive.ofMul ‚Üë(fundSystem K i) := by
          rw [h, QuotientGroup.mk_mul, (QuotientGroup.eq_one_iff _).mpr hŒ∂, one_mul,
            QuotientGroup.mk_prod, ofMul_prod]; rfl
    _ = ‚àë i, (f i) ‚Ä¢ (basisModTorsion K i) := by
          simp_rw [fundSystem, QuotientGroup.out_eq', ofMul_toMul]

/-- **Dirichlet Unit Theorem**. Any unit `x` of `ùìû K` can be written uniquely as the product of
a root of unity and powers of the units of the fundamental system `fundSystem`. -/
theorem exist_unique_eq_mul_prod (x : (ùìû K)À£) : ‚àÉ! Œ∂e : torsion K √ó (Fin (rank K) ‚Üí ‚Ñ§),
    x = Œ∂e.1 * ‚àè i, (fundSystem K i) ^ (Œ∂e.2 i) := by
  let Œ∂ := x * (‚àè i, (fundSystem K i) ^ ((basisModTorsion K).repr (Additive.ofMul ‚Üëx) i))‚Åª¬π
  have h_tors : Œ∂ ‚àà torsion K := by
    rw [‚Üê QuotientGroup.eq_one_iff, QuotientGroup.mk_mul, QuotientGroup.mk_inv, ‚Üê ofMul_eq_zero,
      ofMul_mul, ofMul_inv, QuotientGroup.mk_prod, ofMul_prod]
    simp_rw [QuotientGroup.mk_zpow, ofMul_zpow, fundSystem, QuotientGroup.out_eq']
    rw [add_eq_zero_iff_eq_neg, neg_neg]
    exact ((basisModTorsion K).sum_repr (Additive.ofMul ‚Üëx)).symm
  refine ‚ü®‚ü®‚ü®Œ∂, h_tors‚ü©, ((basisModTorsion K).repr (Additive.ofMul ‚Üëx) : Fin (rank K) ‚Üí ‚Ñ§)‚ü©, ?_, ?_‚ü©
  ¬∑ simp only [Œ∂, _root_.inv_mul_cancel_right]
  ¬∑ rintro ‚ü®‚ü®Œ∂', h_tors'‚ü©, Œ∑‚ü© hf
    simp only [Œ∂, ‚Üê fun_eq_repr K h_tors' hf, Prod.mk.injEq, Subtype.mk.injEq, and_true]
    nth_rewrite 1 [hf]
    rw [_root_.mul_inv_cancel_right]

end statements

end NumberField.Units
