/-
Copyright (c) 2023 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.NumberTheory.NumberField.InfinitePlace.Basic
import Mathlib.RingTheory.LocalRing.RingHom.Basic
import Mathlib.GroupTheory.Torsion

/-!
# Units of a number field

We prove some basic results on the group `(ğ“ K)Ë£` of units of the ring of integers `ğ“ K` of a number
field `K` and its torsion subgroup.

## Main definition

* `NumberField.Units.torsion`: the torsion subgroup of a number field.

## Main results

* `NumberField.isUnit_iff_norm`: an algebraic integer `x : ğ“ K` is a unit if and only if
  `|norm â„š x| = 1`.

* `NumberField.Units.mem_torsion`: a unit `x : (ğ“ K)Ë£` is torsion iff `w x = 1` for all infinite
  places `w` of `K`.

## Tags
number field, units
-/

open scoped NumberField

noncomputable section

open NumberField Units

section Rat

theorem Rat.RingOfIntegers.isUnit_iff {x : ğ“ â„š} : IsUnit x â†” (x : â„š) = 1 âˆ¨ (x : â„š) = -1 := by
  simp_rw [(isUnit_map_iff (Rat.ringOfIntegersEquiv : ğ“ â„š â†’+* â„¤) x).symm, Int.isUnit_iff,
    RingEquiv.coe_toRingHom, RingEquiv.map_eq_one_iff, RingEquiv.map_eq_neg_one_iff, â†
    Subtype.coe_injective.eq_iff]; rfl

end Rat

variable (K : Type*) [Field K]

section IsUnit

variable {K}

theorem NumberField.isUnit_iff_norm [NumberField K] {x : ğ“ K} :
    IsUnit x â†” |(RingOfIntegers.norm â„š x : â„š)| = 1 := by
  convert (RingOfIntegers.isUnit_norm â„š (F := K)).symm
  rw [â† abs_one, abs_eq_abs, â† Rat.RingOfIntegers.isUnit_iff]

end IsUnit

namespace NumberField.Units

section coe

instance : CoeHTC (ğ“ K)Ë£ K :=
  âŸ¨fun x => algebraMap _ K (Units.val x)âŸ©

theorem coe_injective : Function.Injective ((â†‘) : (ğ“ K)Ë£ â†’ K) :=
  RingOfIntegers.coe_injective.comp Units.val_injective

variable {K}

theorem coe_coe (u : (ğ“ K)Ë£) : ((u : ğ“ K) : K) = (u : K) := rfl

theorem coe_mul (x y : (ğ“ K)Ë£) : ((x * y : (ğ“ K)Ë£) : K) = (x : K) * (y : K) := rfl

theorem coe_pow (x : (ğ“ K)Ë£) (n : â„•) : ((x ^ n : (ğ“ K)Ë£) : K) = (x : K) ^ n := by
  rw [â† map_pow, â† val_pow_eq_pow_val]

theorem coe_zpow (x : (ğ“ K)Ë£) (n : â„¤) : (â†‘(x ^ n) : K) = (x : K) ^ n := by
  change ((Units.coeHom K).comp (map (algebraMap (ğ“ K) K))) (x ^ n) = _
  exact map_zpow _ x n

theorem coe_one : ((1 : (ğ“ K)Ë£) : K) = (1 : K) := rfl

theorem coe_neg_one : ((-1 : (ğ“ K)Ë£) : K) = (-1 : K) := rfl

theorem coe_ne_zero (x : (ğ“ K)Ë£) : (x : K) â‰  0 :=
  Subtype.coe_injective.ne_iff.mpr (_root_.Units.ne_zero x)

end coe

open NumberField.InfinitePlace

@[simp]
protected theorem norm [NumberField K] (x : (ğ“ K)Ë£) :
    |Algebra.norm â„š (x : K)| = 1 := by
  rw [â† RingOfIntegers.coe_norm, isUnit_iff_norm.mp x.isUnit]

variable {K} in
theorem pos_at_place (x : (ğ“ K)Ë£) (w : InfinitePlace K) :
    0 < w x := pos_iff.mpr (coe_ne_zero x)

variable {K} in
theorem sum_mult_mul_log [NumberField K] (x : (ğ“ K)Ë£) :
    âˆ‘ w : InfinitePlace K, w.mult * Real.log (w x) = 0 := by
  simpa [Units.norm, Real.log_prod, Real.log_pow] using
    congr_arg Real.log (prod_eq_abs_norm (x : K))

section torsion

/-- The torsion subgroup of the group of units. -/
def torsion : Subgroup (ğ“ K)Ë£ := CommGroup.torsion (ğ“ K)Ë£

theorem mem_torsion {x : (ğ“ K)Ë£} [NumberField K] :
    x âˆˆ torsion K â†” âˆ€ w : InfinitePlace K, w x = 1 := by
  rw [eq_iff_eq (x : K) 1, torsion, CommGroup.mem_torsion]
  refine âŸ¨fun hx Ï† â†¦ (((Ï†.comp <| algebraMap (ğ“ K) K).toMonoidHom.comp <|
    Units.coeHom _).isOfFinOrder hx).norm_eq_one, fun h â†¦ isOfFinOrder_iff_pow_eq_one.2 ?_âŸ©
  obtain âŸ¨n, hn, hxâŸ© := Embeddings.pow_eq_one_of_norm_eq_one K â„‚ x.val.isIntegral_coe h
  exact âŸ¨n, hn, by ext; rw [NumberField.RingOfIntegers.coe_eq_algebraMap, coe_pow, hx,
    NumberField.RingOfIntegers.coe_eq_algebraMap, coe_one]âŸ©

/-- The torsion subgroup is finite. -/
instance [NumberField K] : Fintype (torsion K) := by
  refine @Fintype.ofFinite _ (Set.finite_coe_iff.mpr ?_)
  refine Set.Finite.of_finite_image ?_ (coe_injective K).injOn
  refine (Embeddings.finite_of_norm_le K â„‚ 1).subset
    (fun a âŸ¨u, âŸ¨h_tors, h_uaâŸ©âŸ© => âŸ¨?_, fun Ï† => ?_âŸ©)
  Â· rw [â† h_ua]
    exact u.val.prop
  Â· rw [â† h_ua]
    exact le_of_eq ((eq_iff_eq _ 1).mp ((mem_torsion K).mp h_tors) Ï†)

instance : Nonempty (torsion K) := One.instNonempty

/-- The torsion subgroup is cyclic. -/
instance [NumberField K] : IsCyclic (torsion K) := subgroup_units_cyclic _

/-- The order of the torsion subgroup. -/
def torsionOrder [NumberField K] : â„• := Fintype.card (torsion K)

instance [NumberField K] : NeZero (torsionOrder K) :=
  inferInstanceAs (NeZero (Fintype.card (torsion K)))

theorem torsionOrder_ne_zero [NumberField K] :
    torsionOrder K â‰  0 := NeZero.ne (torsionOrder K)

theorem torsionOrder_pos [NumberField K] :
    0 < torsionOrder K := Nat.pos_of_neZero (torsionOrder K)

/-- If `k` does not divide `torsionOrder` then there are no nontrivial roots of unity of
  order dividing `k`. -/
theorem rootsOfUnity_eq_one [NumberField K] {k : â„•+} (hc : Nat.Coprime k (torsionOrder K))
    {Î¶ : (ğ“ K)Ë£} : Î¶ âˆˆ rootsOfUnity k (ğ“ K) â†” Î¶ = 1 := by
  rw [mem_rootsOfUnity]
  refine âŸ¨fun h => ?_, fun h => by rw [h, one_pow]âŸ©
  refine orderOf_eq_one_iff.mp (Nat.eq_one_of_dvd_coprimes hc ?_ ?_)
  Â· exact orderOf_dvd_of_pow_eq_one h
  Â· have hÎ¶ : Î¶ âˆˆ torsion K := by
      rw [torsion, CommGroup.mem_torsion, isOfFinOrder_iff_pow_eq_one]
      exact âŸ¨k, k.prop, hâŸ©
    rw [orderOf_submonoid (âŸ¨Î¶, hÎ¶âŸ© : torsion K)]
    exact orderOf_dvd_card

/-- The group of roots of unity of order dividing `torsionOrder` is equal to the torsion
group. -/
theorem rootsOfUnity_eq_torsion [NumberField K] :
    rootsOfUnity (torsionOrder K) (ğ“ K) = torsion K := by
  ext Î¶
  rw [torsion, mem_rootsOfUnity]
  refine âŸ¨fun h => ?_, fun h => ?_âŸ©
  Â· rw [CommGroup.mem_torsion, isOfFinOrder_iff_pow_eq_one]
    exact âŸ¨torsionOrder K, torsionOrder_pos K, hâŸ©
  Â· exact Subtype.ext_iff.mp (@pow_card_eq_one (torsion K) _ _ âŸ¨Î¶, hâŸ©)

theorem even_torsionOrder [NumberField K] :
    Even (torsionOrder K) := by
  suffices orderOf (âŸ¨-1, neg_one_mem_torsionâŸ© : torsion K) = 2 by
    rw [even_iff_two_dvd, â† this]
    exact orderOf_dvd_card
  rw [â† Subgroup.orderOf_coe, â† orderOf_units, Units.val_neg, val_one, orderOf_neg_one,
    ringChar.eq_zero, if_neg (by decide)]

section odd

variable {K}

theorem torsion_eq_one_or_neg_one_of_odd_finrank [NumberField K]
    (h : Odd (Module.finrank â„š K)) (x : torsion K) : (x : (ğ“ K)Ë£) = 1 âˆ¨ (x : (ğ“ K)Ë£) = -1 := by
  by_cases hc : 2 < orderOf (x : (ğ“ K)Ë£)
  Â· rw [â† orderOf_units, â† orderOf_submonoid] at hc
    linarith [IsPrimitiveRoot.nrRealPlaces_eq_zero_of_two_lt hc (IsPrimitiveRoot.orderOf (x.1 : K)),
        NumberField.InfinitePlace.nrRealPlaces_pos_of_odd_finrank h]
  Â· push_neg at hc
    interval_cases hi : orderOf (x : (ğ“ K)Ë£)
    Â· linarith [orderOf_pos_iff.2 ((CommGroup.mem_torsion _ x.1).1 x.2)]
    Â· exact Or.intro_left _ (orderOf_eq_one_iff.1 hi)
    Â· rw [â† orderOf_units, CharP.orderOf_eq_two_iff 0 (by decide)] at hi
      simp [â† Units.val_inj, â† Units.val_inj, Units.val_neg, Units.val_one, hi]

theorem torsionOrder_eq_two_of_odd_finrank [NumberField K]
    (h : Odd (Module.finrank â„š K)) : torsionOrder K = 2 := by
  classical
  refine (Finset.card_eq_two.2 âŸ¨1, âŸ¨-1, neg_one_mem_torsionâŸ©,
    by simp [â† Subtype.coe_ne_coe], Finset.ext fun x â†¦ âŸ¨fun _ â†¦ ?_, fun _ â†¦ Finset.mem_univ _âŸ©âŸ©)
  rw [Finset.mem_insert, Finset.mem_singleton, â† Subtype.val_inj, â† Subtype.val_inj]
  exact torsion_eq_one_or_neg_one_of_odd_finrank h x

end odd

end torsion

end Units

end NumberField
