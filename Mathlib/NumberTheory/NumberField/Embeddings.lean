/-
Copyright (c) 2022 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alex J. Best, Xavier Roblot
-/
import Mathlib.Analysis.Complex.Polynomial
import Mathlib.NumberTheory.NumberField.Norm
import Mathlib.NumberTheory.NumberField.Basic
import Mathlib.RingTheory.Norm
import Mathlib.Topology.Instances.Complex
import Mathlib.RingTheory.RootsOfUnity.Basic

#align_import number_theory.number_field.embeddings from "leanprover-community/mathlib"@"caa58cbf5bfb7f81ccbaca4e8b8ac4bc2b39cc1c"

/-!
# Embeddings of number fields
This file defines the embeddings of a number field into an algebraic closed field.

## Main Definitions and Results
* `NumberField.Embeddings.range_eval_eq_rootSet_minpoly`: let `x âˆˆ K` with `K` number field and
  let `A` be an algebraic closed field of char. 0, then the images of `x` by the embeddings of `K`
   in `A` are exactly the roots in `A` of the minimal polynomial of `x` over `â„š`.
* `NumberField.Embeddings.pow_eq_one_of_norm_eq_one`: an algebraic integer whose conjugates are
  all of norm one is a root of unity.
* `NumberField.InfinitePlace`: the type of infinite places of a number field `K`.
* `NumberField.InfinitePlace.mk_eq_iff`: two complex embeddings define the same infinite place iff
they are equal or complex conjugates.
* `NumberField.InfinitePlace.prod_eq_abs_norm`: the infinite part of the product formula, that is
for `x âˆˆ K`, we have `Î _w â€–xâ€–_w = |norm(x)|` where the product is over the infinite place `w` and
`â€–Â·â€–_w` is the normalized absolute value for `w`.

## Tags
number field, embeddings, places, infinite places
-/

open scoped Classical

namespace NumberField.Embeddings

section Fintype

open FiniteDimensional

variable (K : Type*) [Field K] [NumberField K]
variable (A : Type*) [Field A] [CharZero A]

/-- There are finitely many embeddings of a number field. -/
noncomputable instance : Fintype (K â†’+* A) :=
  Fintype.ofEquiv (K â†’â‚[â„š] A) RingHom.equivRatAlgHom.symm

variable [IsAlgClosed A]

/-- The number of embeddings of a number field is equal to its finrank. -/
theorem card : Fintype.card (K â†’+* A) = finrank â„š K := by
  rw [Fintype.ofEquiv_card RingHom.equivRatAlgHom.symm, AlgHom.card]
#align number_field.embeddings.card NumberField.Embeddings.card

instance : Nonempty (K â†’+* A) := by
  rw [â† Fintype.card_pos_iff, NumberField.Embeddings.card K A]
  exact FiniteDimensional.finrank_pos

end Fintype

section Roots

open Set Polynomial

variable (K A : Type*) [Field K] [NumberField K] [Field A] [Algebra â„š A] [IsAlgClosed A] (x : K)

/-- Let `A` be an algebraically closed field and let `x âˆˆ K`, with `K` a number field.
The images of `x` by the embeddings of `K` in `A` are exactly the roots in `A` of
the minimal polynomial of `x` over `â„š`. -/
theorem range_eval_eq_rootSet_minpoly :
    (range fun Ï† : K â†’+* A => Ï† x) = (minpoly â„š x).rootSet A := by
  convert (NumberField.isAlgebraic K).range_eval_eq_rootSet_minpoly A x using 1
  ext a
  exact âŸ¨fun âŸ¨Ï†, hÏ†âŸ© => âŸ¨Ï†.toRatAlgHom, hÏ†âŸ©, fun âŸ¨Ï†, hÏ†âŸ© => âŸ¨Ï†.toRingHom, hÏ†âŸ©âŸ©
#align number_field.embeddings.range_eval_eq_root_set_minpoly NumberField.Embeddings.range_eval_eq_rootSet_minpoly

end Roots

section Bounded

open FiniteDimensional Polynomial Set

variable {K : Type*} [Field K] [NumberField K]
variable {A : Type*} [NormedField A] [IsAlgClosed A] [NormedAlgebra â„š A]

theorem coeff_bdd_of_norm_le {B : â„} {x : K} (h : âˆ€ Ï† : K â†’+* A, â€–Ï† xâ€– â‰¤ B) (i : â„•) :
    â€–(minpoly â„š x).coeff iâ€– â‰¤ max B 1 ^ finrank â„š K * (finrank â„š K).choose (finrank â„š K / 2) := by
  have hx := IsSeparable.isIntegral â„š x
  rw [â† norm_algebraMap' A, â† coeff_map (algebraMap â„š A)]
  refine coeff_bdd_of_roots_le _ (minpoly.monic hx)
      (IsAlgClosed.splits_codomain _) (minpoly.natDegree_le x) (fun z hz => ?_) i
  classical
  rw [â† Multiset.mem_toFinset] at hz
  obtain âŸ¨Ï†, rflâŸ© := (range_eval_eq_rootSet_minpoly K A x).symm.subset hz
  exact h Ï†
#align number_field.embeddings.coeff_bdd_of_norm_le NumberField.Embeddings.coeff_bdd_of_norm_le

variable (K A)

/-- Let `B` be a real number. The set of algebraic integers in `K` whose conjugates are all
smaller in norm than `B` is finite. -/
theorem finite_of_norm_le (B : â„) : {x : K | IsIntegral â„¤ x âˆ§ âˆ€ Ï† : K â†’+* A, â€–Ï† xâ€– â‰¤ B}.Finite := by
  let C := Nat.ceil (max B 1 ^ finrank â„š K * (finrank â„š K).choose (finrank â„š K / 2))
  have := bUnion_roots_finite (algebraMap â„¤ K) (finrank â„š K) (finite_Icc (-C : â„¤) C)
  refine this.subset fun x hx => ?_; simp_rw [mem_iUnion]
  have h_map_â„š_minpoly := minpoly.isIntegrallyClosed_eq_field_fractions' â„š hx.1
  refine âŸ¨_, âŸ¨?_, fun i => ?_âŸ©, mem_rootSet.2 âŸ¨minpoly.ne_zero hx.1, minpoly.aeval â„¤ xâŸ©âŸ©
  Â· rw [â† (minpoly.monic hx.1).natDegree_map (algebraMap â„¤ â„š), â† h_map_â„š_minpoly]
    exact minpoly.natDegree_le x
  rw [mem_Icc, â† abs_le, â† @Int.cast_le â„]
  refine (Eq.trans_le ?_ <| coeff_bdd_of_norm_le hx.2 i).trans (Nat.le_ceil _)
  rw [h_map_â„š_minpoly, coeff_map, eq_intCast, Int.norm_cast_rat, Int.norm_eq_abs, Int.cast_abs]
#align number_field.embeddings.finite_of_norm_le NumberField.Embeddings.finite_of_norm_le

/-- An algebraic integer whose conjugates are all of norm one is a root of unity. -/
theorem pow_eq_one_of_norm_eq_one {x : K} (hxi : IsIntegral â„¤ x) (hx : âˆ€ Ï† : K â†’+* A, â€–Ï† xâ€– = 1) :
    âˆƒ (n : â„•) (_ : 0 < n), x ^ n = 1 := by
  obtain âŸ¨a, -, b, -, habne, hâŸ© :=
    @Set.Infinite.exists_ne_map_eq_of_mapsTo _ _ _ _ (x ^ Â· : â„• â†’ K) Set.infinite_univ
      (by exact fun a _ => âŸ¨hxi.pow a, fun Ï† => by simp [hx Ï†]âŸ©) (finite_of_norm_le K A (1 : â„))
  wlog hlt : b < a
  Â· exact this K A hxi hx b a habne.symm h.symm (habne.lt_or_lt.resolve_right hlt)
  refine âŸ¨a - b, tsub_pos_of_lt hlt, ?_âŸ©
  rw [â† Nat.sub_add_cancel hlt.le, pow_add, mul_left_eq_selfâ‚€] at h
  refine h.resolve_right fun hp => ?_
  specialize hx (IsAlgClosed.lift (R := â„š)).toRingHom
  rw [pow_eq_zero hp, map_zero, norm_zero] at hx; norm_num at hx
#align number_field.embeddings.pow_eq_one_of_norm_eq_one NumberField.Embeddings.pow_eq_one_of_norm_eq_one

end Bounded

end NumberField.Embeddings

section Place

variable {K : Type*} [Field K] {A : Type*} [NormedDivisionRing A] [Nontrivial A] (Ï† : K â†’+* A)

/-- An embedding into a normed division ring defines a place of `K` -/
def NumberField.place : AbsoluteValue K â„ :=
  (IsAbsoluteValue.toAbsoluteValue (norm : A â†’ â„)).comp Ï†.injective
#align number_field.place NumberField.place

@[simp]
theorem NumberField.place_apply (x : K) : (NumberField.place Ï†) x = norm (Ï† x) := rfl
#align number_field.place_apply NumberField.place_apply

end Place

namespace NumberField.ComplexEmbedding

open Complex NumberField

open scoped ComplexConjugate

variable {K : Type*} [Field K] {k : Type*} [Field k]

/-- The conjugate of a complex embedding as a complex embedding. -/
abbrev conjugate (Ï† : K â†’+* â„‚) : K â†’+* â„‚ := star Ï†
#align number_field.complex_embedding.conjugate NumberField.ComplexEmbedding.conjugate

@[simp]
theorem conjugate_coe_eq (Ï† : K â†’+* â„‚) (x : K) : (conjugate Ï†) x = conj (Ï† x) := rfl
#align number_field.complex_embedding.conjugate_coe_eq NumberField.ComplexEmbedding.conjugate_coe_eq

theorem place_conjugate (Ï† : K â†’+* â„‚) : place (conjugate Ï†) = place Ï† := by
  ext; simp only [place_apply, norm_eq_abs, abs_conj, conjugate_coe_eq]
#align number_field.complex_embedding.place_conjugate NumberField.ComplexEmbedding.place_conjugate

/-- An embedding into `â„‚` is real if it is fixed by complex conjugation. -/
abbrev IsReal (Ï† : K â†’+* â„‚) : Prop := IsSelfAdjoint Ï†
#align number_field.complex_embedding.is_real NumberField.ComplexEmbedding.IsReal

theorem isReal_iff {Ï† : K â†’+* â„‚} : IsReal Ï† â†” conjugate Ï† = Ï† := isSelfAdjoint_iff
#align number_field.complex_embedding.is_real_iff NumberField.ComplexEmbedding.isReal_iff

theorem isReal_conjugate_iff {Ï† : K â†’+* â„‚} : IsReal (conjugate Ï†) â†” IsReal Ï† :=
  IsSelfAdjoint.star_iff
#align number_field.complex_embedding.is_real_conjugate_iff NumberField.ComplexEmbedding.isReal_conjugate_iff

/-- A real embedding as a ring homomorphism from `K` to `â„` . -/
def IsReal.embedding {Ï† : K â†’+* â„‚} (hÏ† : IsReal Ï†) : K â†’+* â„ where
  toFun x := (Ï† x).re
  map_one' := by simp only [map_one, one_re]
  map_mul' := by
    simp only [Complex.conj_eq_iff_im.mp (RingHom.congr_fun hÏ† _), map_mul, mul_re,
      mul_zero, tsub_zero, eq_self_iff_true, forall_const]
  map_zero' := by simp only [map_zero, zero_re]
  map_add' := by simp only [map_add, add_re, eq_self_iff_true, forall_const]
#align number_field.complex_embedding.is_real.embedding NumberField.ComplexEmbedding.IsReal.embedding

@[simp]
theorem IsReal.coe_embedding_apply {Ï† : K â†’+* â„‚} (hÏ† : IsReal Ï†) (x : K) :
    (hÏ†.embedding x : â„‚) = Ï† x := by
  apply Complex.ext
  Â· rfl
  Â· rw [ofReal_im, eq_comm, â† Complex.conj_eq_iff_im]
    exact RingHom.congr_fun hÏ† x
#align number_field.complex_embedding.is_real.coe_embedding_apply NumberField.ComplexEmbedding.IsReal.coe_embedding_apply

lemma IsReal.comp (f : k â†’+* K) {Ï† : K â†’+* â„‚} (hÏ† : IsReal Ï†) :
    IsReal (Ï†.comp f) := by ext1 x; simpa using RingHom.congr_fun hÏ† (f x)

lemma isReal_comp_iff {f : k â‰ƒ+* K} {Ï† : K â†’+* â„‚} :
    IsReal (Ï†.comp (f : k â†’+* K)) â†” IsReal Ï† :=
  âŸ¨fun H â†¦ by convert H.comp f.symm.toRingHom; ext1; simp, IsReal.comp _âŸ©

lemma exists_comp_symm_eq_of_comp_eq [Algebra k K] [IsGalois k K] (Ï† Ïˆ : K â†’+* â„‚)
    (h : Ï†.comp (algebraMap k K) = Ïˆ.comp (algebraMap k K)) :
    âˆƒ Ïƒ : K â‰ƒâ‚[k] K, Ï†.comp Ïƒ.symm = Ïˆ := by
  letI := (Ï†.comp (algebraMap k K)).toAlgebra
  letI := Ï†.toAlgebra
  have : IsScalarTower k K â„‚ := IsScalarTower.of_algebraMap_eq' rfl
  let Ïˆ' : K â†’â‚[k] â„‚ := { Ïˆ with commutes' := fun r â†¦ (RingHom.congr_fun h r).symm }
  use (AlgHom.restrictNormal' Ïˆ' K).symm
  ext1 x
  exact AlgHom.restrictNormal_commutes Ïˆ' K x

variable [Algebra k K] (Ï† : K â†’+* â„‚) (Ïƒ : K â‰ƒâ‚[k] K)

/--
`IsConj Ï† Ïƒ` states that `Ïƒ : K â‰ƒâ‚[k] K` is the conjugation under the embedding `Ï† : K â†’+* â„‚`.
-/
def IsConj : Prop := conjugate Ï† = Ï†.comp Ïƒ

variable {Ï† Ïƒ}

lemma IsConj.eq (h : IsConj Ï† Ïƒ) (x) : Ï† (Ïƒ x) = star (Ï† x) := RingHom.congr_fun h.symm x

lemma IsConj.ext {Ïƒâ‚ Ïƒâ‚‚ : K â‰ƒâ‚[k] K} (hâ‚ : IsConj Ï† Ïƒâ‚) (hâ‚‚ : IsConj Ï† Ïƒâ‚‚) : Ïƒâ‚ = Ïƒâ‚‚ :=
  AlgEquiv.ext fun x â†¦ Ï†.injective ((hâ‚.eq x).trans (hâ‚‚.eq x).symm)

lemma IsConj.ext_iff {Ïƒâ‚ Ïƒâ‚‚ : K â‰ƒâ‚[k] K} (hâ‚ : IsConj Ï† Ïƒâ‚) : Ïƒâ‚ = Ïƒâ‚‚ â†” IsConj Ï† Ïƒâ‚‚ :=
  âŸ¨fun e â†¦ e â–¸ hâ‚, hâ‚.extâŸ©

lemma IsConj.isReal_comp (h : IsConj Ï† Ïƒ) : IsReal (Ï†.comp (algebraMap k K)) := by
  ext1 x
  simp only [conjugate_coe_eq, RingHom.coe_comp, Function.comp_apply, â† h.eq,
    starRingEnd_apply, AlgEquiv.commutes]

lemma isConj_one_iff : IsConj Ï† (1 : K â‰ƒâ‚[k] K) â†” IsReal Ï† := Iff.rfl

alias âŸ¨_, IsReal.isConjGal_oneâŸ© := ComplexEmbedding.isConj_one_iff

lemma IsConj.symm (hÏƒ : IsConj Ï† Ïƒ) :
    IsConj Ï† Ïƒ.symm := RingHom.ext fun x â†¦ by simpa using congr_arg star (hÏƒ.eq (Ïƒ.symm x))

lemma isConj_symm : IsConj Ï† Ïƒ.symm â†” IsConj Ï† Ïƒ :=
  âŸ¨IsConj.symm, IsConj.symmâŸ©

end NumberField.ComplexEmbedding

section InfinitePlace

open NumberField

variable {k : Type*} [Field k] (K : Type*) [Field K] {F : Type*} [Field F]

/-- An infinite place of a number field `K` is a place associated to a complex embedding. -/
def NumberField.InfinitePlace := { w : AbsoluteValue K â„ // âˆƒ Ï† : K â†’+* â„‚, place Ï† = w }
#align number_field.infinite_place NumberField.InfinitePlace

instance [NumberField K] : Nonempty (NumberField.InfinitePlace K) := Set.instNonemptyRange _

variable {K}

/-- Return the infinite place defined by a complex embedding `Ï†`. -/
noncomputable def NumberField.InfinitePlace.mk (Ï† : K â†’+* â„‚) : NumberField.InfinitePlace K :=
  âŸ¨place Ï†, âŸ¨Ï†, rflâŸ©âŸ©
#align number_field.infinite_place.mk NumberField.InfinitePlace.mk

namespace NumberField.InfinitePlace

open NumberField

instance {K : Type*} [Field K] : FunLike (InfinitePlace K) K â„ where
  coe w x := w.1 x
  coe_injective' := fun _ _ h => Subtype.eq (AbsoluteValue.ext fun x => congr_fun h x)

instance : MonoidWithZeroHomClass (InfinitePlace K) K â„ where
  map_mul w _ _ := w.1.map_mul _ _
  map_one w := w.1.map_one
  map_zero w := w.1.map_zero

instance : NonnegHomClass (InfinitePlace K) K â„ where
  apply_nonneg w _ := w.1.nonneg _

@[simp]
theorem apply (Ï† : K â†’+* â„‚) (x : K) : (mk Ï†) x = Complex.abs (Ï† x) := rfl
#align number_field.infinite_place.apply NumberField.InfinitePlace.apply

/-- For an infinite place `w`, return an embedding `Ï†` such that `w = infinite_place Ï†` . -/
noncomputable def embedding (w : InfinitePlace K) : K â†’+* â„‚ := w.2.choose
#align number_field.infinite_place.embedding NumberField.InfinitePlace.embedding

@[simp]
theorem mk_embedding (w : InfinitePlace K) : mk (embedding w) = w := Subtype.ext w.2.choose_spec
#align number_field.infinite_place.mk_embedding NumberField.InfinitePlace.mk_embedding

@[simp]
theorem mk_conjugate_eq (Ï† : K â†’+* â„‚) : mk (ComplexEmbedding.conjugate Ï†) = mk Ï† := by
  refine DFunLike.ext _ _ (fun x => ?_)
  rw [apply, apply, ComplexEmbedding.conjugate_coe_eq, Complex.abs_conj]
#align number_field.infinite_place.mk_conjugate_eq NumberField.InfinitePlace.mk_conjugate_eq

theorem norm_embedding_eq (w : InfinitePlace K) (x : K) :
    â€–(embedding w) xâ€– = w x := by
  nth_rewrite 2 [â† mk_embedding w]
  rfl

theorem eq_iff_eq (x : K) (r : â„) : (âˆ€ w : InfinitePlace K, w x = r) â†” âˆ€ Ï† : K â†’+* â„‚, â€–Ï† xâ€– = r :=
  âŸ¨fun hw Ï† => hw (mk Ï†), by rintro hÏ† âŸ¨w, âŸ¨Ï†, rflâŸ©âŸ©; exact hÏ† Ï†âŸ©
#align number_field.infinite_place.eq_iff_eq NumberField.InfinitePlace.eq_iff_eq

theorem le_iff_le (x : K) (r : â„) : (âˆ€ w : InfinitePlace K, w x â‰¤ r) â†” âˆ€ Ï† : K â†’+* â„‚, â€–Ï† xâ€– â‰¤ r :=
  âŸ¨fun hw Ï† => hw (mk Ï†), by rintro hÏ† âŸ¨w, âŸ¨Ï†, rflâŸ©âŸ©; exact hÏ† Ï†âŸ©
#align number_field.infinite_place.le_iff_le NumberField.InfinitePlace.le_iff_le

theorem pos_iff {w : InfinitePlace K} {x : K} : 0 < w x â†” x â‰  0 := AbsoluteValue.pos_iff w.1
#align number_field.infinite_place.pos_iff NumberField.InfinitePlace.pos_iff

@[simp]
theorem mk_eq_iff {Ï† Ïˆ : K â†’+* â„‚} : mk Ï† = mk Ïˆ â†” Ï† = Ïˆ âˆ¨ ComplexEmbedding.conjugate Ï† = Ïˆ := by
  constructor
  Â· -- We prove that the map Ïˆ âˆ˜ Ï†â»Â¹ between Ï†(K) and â„‚ is uniform continuous, thus it is either the
    -- inclusion or the complex conjugation using `Complex.uniformContinuous_ringHom_eq_id_or_conj`
    intro hâ‚€
    obtain âŸ¨j, hiÏ†âŸ© := (Ï†.injective).hasLeftInverse
    let Î¹ := RingEquiv.ofLeftInverse hiÏ†
    have hlip : LipschitzWith 1 (RingHom.comp Ïˆ Î¹.symm.toRingHom) := by
      change LipschitzWith 1 (Ïˆ âˆ˜ Î¹.symm)
      apply LipschitzWith.of_dist_le_mul
      intro x y
      rw [NNReal.coe_one, one_mul, NormedField.dist_eq, Function.comp_apply, Function.comp_apply,
        â† map_sub, â† map_sub]
      apply le_of_eq
      suffices â€–Ï† (Î¹.symm (x - y))â€– = â€–Ïˆ (Î¹.symm (x - y))â€– by
        rw [â† this, â† RingEquiv.ofLeftInverse_apply hiÏ† _, RingEquiv.apply_symm_apply Î¹ _]
        rfl
      exact congrFun (congrArg (â†‘) hâ‚€) _
    cases
      Complex.uniformContinuous_ringHom_eq_id_or_conj Ï†.fieldRange hlip.uniformContinuous with
    | inl h =>
        left; ext1 x
        conv_rhs => rw [â† hiÏ† x]
        exact (congrFun h (Î¹ x)).symm
    | inr h =>
        right; ext1 x
        conv_rhs => rw [â† hiÏ† x]
        exact (congrFun h (Î¹ x)).symm
  Â· rintro (âŸ¨hâŸ© | âŸ¨hâŸ©)
    Â· exact congr_arg mk h
    Â· rw [â† mk_conjugate_eq]
      exact congr_arg mk h
#align number_field.infinite_place.mk_eq_iff NumberField.InfinitePlace.mk_eq_iff

/-- An infinite place is real if it is defined by a real embedding. -/
def IsReal (w : InfinitePlace K) : Prop := âˆƒ Ï† : K â†’+* â„‚, ComplexEmbedding.IsReal Ï† âˆ§ mk Ï† = w
#align number_field.infinite_place.is_real NumberField.InfinitePlace.IsReal

/-- An infinite place is complex if it is defined by a complex (ie. not real) embedding. -/
def IsComplex (w : InfinitePlace K) : Prop := âˆƒ Ï† : K â†’+* â„‚, Â¬ComplexEmbedding.IsReal Ï† âˆ§ mk Ï† = w
#align number_field.infinite_place.is_complex NumberField.InfinitePlace.IsComplex

theorem embedding_mk_eq (Ï† : K â†’+* â„‚) :
    embedding (mk Ï†) = Ï† âˆ¨ embedding (mk Ï†) = ComplexEmbedding.conjugate Ï† := by
  rw [@eq_comm _ _ Ï†, @eq_comm _ _ (ComplexEmbedding.conjugate Ï†), â† mk_eq_iff, mk_embedding]

@[simp]
theorem embedding_mk_eq_of_isReal {Ï† : K â†’+* â„‚} (h : ComplexEmbedding.IsReal Ï†) :
    embedding (mk Ï†) = Ï† := by
  have := embedding_mk_eq Ï†
  rwa [ComplexEmbedding.isReal_iff.mp h, or_self] at this
#align number_field.complex_embeddings.is_real.embedding_mk NumberField.InfinitePlace.embedding_mk_eq_of_isReal

theorem isReal_iff {w : InfinitePlace K} :
    IsReal w â†” ComplexEmbedding.IsReal (embedding w) := by
  refine âŸ¨?_, fun h => âŸ¨embedding w, h, mk_embedding wâŸ©âŸ©
  rintro âŸ¨Ï†, âŸ¨hÏ†, rflâŸ©âŸ©
  rwa [embedding_mk_eq_of_isReal hÏ†]
#align number_field.infinite_place.is_real_iff NumberField.InfinitePlace.isReal_iff

theorem isComplex_iff {w : InfinitePlace K} :
    IsComplex w â†” Â¬ComplexEmbedding.IsReal (embedding w) := by
  refine âŸ¨?_, fun h => âŸ¨embedding w, h, mk_embedding wâŸ©âŸ©
  rintro âŸ¨Ï†, âŸ¨hÏ†, rflâŸ©âŸ©
  contrapose! hÏ†
  cases mk_eq_iff.mp (mk_embedding (mk Ï†)) with
  | inl h => rwa [h] at hÏ†
  | inr h => rwa [â† ComplexEmbedding.isReal_conjugate_iff, h] at hÏ†
#align number_field.infinite_place.is_complex_iff NumberField.InfinitePlace.isComplex_iff

@[simp]
theorem conjugate_embedding_eq_of_isReal {w : InfinitePlace K} (h : IsReal w) :
    ComplexEmbedding.conjugate (embedding w) = embedding w :=
  ComplexEmbedding.isReal_iff.mpr (isReal_iff.mp h)

@[simp]
theorem not_isReal_iff_isComplex {w : InfinitePlace K} : Â¬IsReal w â†” IsComplex w := by
  rw [isComplex_iff, isReal_iff]
#align number_field.infinite_place.not_is_real_iff_is_complex NumberField.InfinitePlace.not_isReal_iff_isComplex

@[simp]
theorem not_isComplex_iff_isReal {w : InfinitePlace K} : Â¬IsComplex w â†” IsReal w := by
  rw [isComplex_iff, isReal_iff, not_not]

theorem isReal_or_isComplex (w : InfinitePlace K) : IsReal w âˆ¨ IsComplex w := by
  rw [â† not_isReal_iff_isComplex]; exact em _
#align number_field.infinite_place.is_real_or_is_complex NumberField.InfinitePlace.isReal_or_isComplex

theorem ne_of_isReal_isComplex {w w' : InfinitePlace K} (h : IsReal w) (h' : IsComplex w') :
    w â‰  w' := fun h_eq â†¦ not_isReal_iff_isComplex.mpr h' (h_eq â–¸ h)

variable (K) in
theorem disjoint_isReal_isComplex :
    Disjoint {(w : InfinitePlace K) | IsReal w} {(w : InfinitePlace K) | IsComplex w} :=
  Set.disjoint_iff.2 <| fun _ hw â†¦ not_isReal_iff_isComplex.2 hw.2 hw.1

/-- The real embedding associated to a real infinite place. -/
noncomputable def embedding_of_isReal {w : InfinitePlace K} (hw : IsReal w) : K â†’+* â„ :=
  ComplexEmbedding.IsReal.embedding (isReal_iff.mp hw)
#align number_field.infinite_place.is_real.embedding NumberField.InfinitePlace.embedding_of_isReal

@[simp]
theorem embedding_of_isReal_apply {w : InfinitePlace K} (hw : IsReal w) (x : K) :
    ((embedding_of_isReal hw) x : â„‚) = (embedding w) x :=
  ComplexEmbedding.IsReal.coe_embedding_apply (isReal_iff.mp hw) x

theorem norm_embedding_of_isReal {w : InfinitePlace K} (hw : IsReal w) (x : K) :
    â€–embedding_of_isReal hw xâ€– = w x := by
  rw [â† norm_embedding_eq, â† embedding_of_isReal_apply hw, Complex.norm_real]

@[simp]
theorem isReal_of_mk_isReal {Ï† : K â†’+* â„‚} (h : IsReal (mk Ï†)) :
    ComplexEmbedding.IsReal Ï† := by
  contrapose! h
  rw [not_isReal_iff_isComplex]
  exact âŸ¨Ï†, h, rflâŸ©

lemma isReal_mk_iff {Ï† : K â†’+* â„‚} :
    IsReal (mk Ï†) â†” ComplexEmbedding.IsReal Ï† :=
  âŸ¨isReal_of_mk_isReal, fun H â†¦ âŸ¨_, H, rflâŸ©âŸ©

lemma isComplex_mk_iff {Ï† : K â†’+* â„‚} :
    IsComplex (mk Ï†) â†” Â¬ ComplexEmbedding.IsReal Ï† :=
  not_isReal_iff_isComplex.symm.trans isReal_mk_iff.not

@[simp]
theorem not_isReal_of_mk_isComplex {Ï† : K â†’+* â„‚} (h : IsComplex (mk Ï†)) :
    Â¬ ComplexEmbedding.IsReal Ï† := by rwa [â† isComplex_mk_iff]

/-- The multiplicity of an infinite place, that is the number of distinct complex embeddings that
define it, see `card_filter_mk_eq`. -/
noncomputable def mult (w : InfinitePlace K) : â„• := if (IsReal w) then 1 else 2

theorem mult_pos {w : InfinitePlace K} : 0 < mult w := by
  rw [mult]
  split_ifs <;> norm_num

@[simp]
theorem mult_ne_zero {w : InfinitePlace K} : mult w â‰  0 := ne_of_gt mult_pos

theorem one_le_mult {w : InfinitePlace K} : (1 : â„) â‰¤ mult w := by
  rw [â† Nat.cast_one, Nat.cast_le]
  exact mult_pos

theorem card_filter_mk_eq [NumberField K] (w : InfinitePlace K) :
    (Finset.univ.filter fun Ï† => mk Ï† = w).card = mult w := by
  conv_lhs =>
    congr; congr; ext
    rw [â† mk_embedding w, mk_eq_iff, ComplexEmbedding.conjugate, star_involutive.eq_iff]
  simp_rw [Finset.filter_or, Finset.filter_eq' _ (embedding w),
    Finset.filter_eq' _ (ComplexEmbedding.conjugate (embedding w)),
    Finset.mem_univ, ite_true, mult]
  split_ifs with hw
  Â· rw [ComplexEmbedding.isReal_iff.mp (isReal_iff.mp hw), Finset.union_idempotent,
      Finset.card_singleton]
  Â· refine Finset.card_pair ?_
    rwa [Ne, eq_comm, â† ComplexEmbedding.isReal_iff, â† isReal_iff]

noncomputable instance NumberField.InfinitePlace.fintype [NumberField K] :
    Fintype (InfinitePlace K) := Set.fintypeRange _
#align number_field.infinite_place.number_field.infinite_place.fintype NumberField.InfinitePlace.NumberField.InfinitePlace.fintype

theorem sum_mult_eq [NumberField K] :
    âˆ‘ w : InfinitePlace K, mult w = FiniteDimensional.finrank â„š K := by
  rw [â† Embeddings.card K â„‚, Fintype.card, Finset.card_eq_sum_ones, â† Finset.univ.sum_fiberwise
    (fun Ï† => InfinitePlace.mk Ï†)]
  exact Finset.sum_congr rfl
    (fun _ _ => by rw [Finset.sum_const, smul_eq_mul, mul_one, card_filter_mk_eq])

/-- The map from real embeddings to real infinite places as an equiv -/
noncomputable def mkReal :
    { Ï† : K â†’+* â„‚ // ComplexEmbedding.IsReal Ï† } â‰ƒ { w : InfinitePlace K // IsReal w } := by
  refine (Equiv.ofBijective (fun Ï† => âŸ¨mk Ï†, ?_âŸ©) âŸ¨fun Ï† Ïˆ h => ?_, fun w => ?_âŸ©)
  Â· exact âŸ¨Ï†, Ï†.prop, rflâŸ©
  Â· rwa [Subtype.mk.injEq, mk_eq_iff, ComplexEmbedding.isReal_iff.mp Ï†.prop, or_self,
      â† Subtype.ext_iff] at h
  Â· exact âŸ¨âŸ¨embedding w, isReal_iff.mp w.propâŸ©, by simpâŸ©

/-- The map from nonreal embeddings to complex infinite places -/
noncomputable def mkComplex :
    { Ï† : K â†’+* â„‚ // Â¬ComplexEmbedding.IsReal Ï† } â†’ { w : InfinitePlace K // IsComplex w } :=
  Subtype.map mk fun Ï† hÏ† => âŸ¨Ï†, hÏ†, rflâŸ©
#align number_field.infinite_place.mk_complex NumberField.InfinitePlace.mkComplex

@[simp]
theorem mkReal_coe (Ï† : { Ï† : K â†’+* â„‚ // ComplexEmbedding.IsReal Ï† }) :
    (mkReal Ï† : InfinitePlace K) = mk (Ï† : K â†’+* â„‚) := rfl
#align number_field.infinite_place.mk_real_coe NumberField.InfinitePlace.mkReal_coe

@[simp]
theorem mkComplex_coe (Ï† : { Ï† : K â†’+* â„‚ // Â¬ComplexEmbedding.IsReal Ï† }) :
    (mkComplex Ï† : InfinitePlace K) = mk (Ï† : K â†’+* â„‚) := rfl
#align number_field.infinite_place.mk_complex_coe NumberField.InfinitePlace.mkComplex_coe

variable [NumberField K]

/-- The infinite part of the product formula : for `x âˆˆ K`, we have `Î _w â€–xâ€–_w = |norm(x)|` where
`â€–Â·â€–_w` is the normalized absolute value for `w`.  -/
theorem prod_eq_abs_norm (x : K) :
    âˆ w : InfinitePlace K, w x ^ mult w = abs (Algebra.norm â„š x) := by
  convert (congr_arg Complex.abs (@Algebra.norm_eq_prod_embeddings â„š _ _ _ _ â„‚ _ _ _ _ _ x)).symm
  Â· rw [map_prod, â† Fintype.prod_equiv RingHom.equivRatAlgHom (fun f => Complex.abs (f x))
      (fun Ï† => Complex.abs (Ï† x)) fun _ => by simp [RingHom.equivRatAlgHom_apply]; rfl]
    rw [â† Finset.prod_fiberwise Finset.univ (fun Ï† => mk Ï†) (fun Ï† => Complex.abs (Ï† x))]
    have : âˆ€ w : InfinitePlace K, âˆ€ Ï† âˆˆ Finset.filter (fun a â†¦ mk a = w) Finset.univ,
        Complex.abs (Ï† x) = w x := by
      intro _ _ hÏ†
      rw [â† (Finset.mem_filter.mp hÏ†).2]
      rfl
    simp_rw [Finset.prod_congr rfl (this _), Finset.prod_const, card_filter_mk_eq]
  Â· rw [eq_ratCast, Rat.cast_abs, â† Complex.abs_ofReal, Complex.ofReal_ratCast]
#align number_field.infinite_place.prod_eq_abs_norm NumberField.InfinitePlace.prod_eq_abs_norm

theorem one_le_of_lt_one {w : InfinitePlace K} {a : (ğ“ K)} (ha : a â‰  0)
    (h : âˆ€ â¦ƒzâ¦„, z â‰  w â†’ z a < 1) : 1 â‰¤ w a := by
  suffices (1:â„) â‰¤ |Algebra.norm â„š (a : K)| by
    contrapose! this
    rw [â† InfinitePlace.prod_eq_abs_norm, â† Finset.prod_const_one]
    refine Finset.prod_lt_prod_of_nonempty (fun _ _ â†¦ ?_) (fun z _ â†¦ ?_) Finset.univ_nonempty
    Â· exact pow_pos (pos_iff.mpr ((Subalgebra.coe_eq_zero _).not.mpr ha)) _
    Â· refine pow_lt_one (apply_nonneg _ _) ?_ (by rw [mult]; split_ifs <;> norm_num)
      by_cases hz : z = w
      Â· rwa [hz]
      Â· exact h hz
  rw [â† Algebra.coe_norm_int, â† Int.cast_one, â† Int.cast_abs, Rat.cast_intCast, Int.cast_le]
  exact Int.one_le_abs (Algebra.norm_ne_zero_iff.mpr ha)

open scoped IntermediateField in
theorem _root_.NumberField.is_primitive_element_of_infinitePlace_lt {x : ğ“ K}
    {w : InfinitePlace K} (hâ‚ : x â‰  0) (hâ‚‚ : âˆ€ â¦ƒw'â¦„, w' â‰  w â†’ w' x < 1)
    (hâ‚ƒ : IsReal w âˆ¨ |(w.embedding x).re| < 1) : â„šâŸ®(x : K)âŸ¯ = âŠ¤ := by
  rw [Field.primitive_element_iff_algHom_eq_of_eval â„š â„‚ ?_ _ w.embedding.toRatAlgHom]
  Â· intro Ïˆ hÏˆ
    have h : 1 â‰¤ w x := one_le_of_lt_one hâ‚ hâ‚‚
    have main : w = InfinitePlace.mk Ïˆ.toRingHom := by
      erw [â† norm_embedding_eq, hÏˆ] at h
      contrapose! h
      exact hâ‚‚ h.symm
    rw [(mk_embedding w).symm, mk_eq_iff] at main
    cases hâ‚ƒ with
    | inl hw =>
      rw [conjugate_embedding_eq_of_isReal hw, or_self] at main
      exact congr_arg RingHom.toRatAlgHom main
    | inr hw =>
      refine congr_arg RingHom.toRatAlgHom (main.resolve_right fun h' â†¦ hw.not_le ?_)
      have : (embedding w x).im = 0 := by
        erw [â† Complex.conj_eq_iff_im, RingHom.congr_fun h' x]
        exact hÏˆ.symm
      rwa [â† norm_embedding_eq, â† Complex.re_add_im (embedding w x), this, Complex.ofReal_zero,
        zero_mul, add_zero, Complex.norm_eq_abs, Complex.abs_ofReal] at h
  Â· exact fun x â†¦ IsAlgClosed.splits_codomain (minpoly â„š x)

theorem _root_.NumberField.adjoin_eq_top_of_infinitePlace_lt {x : ğ“ K} {w : InfinitePlace K}
    (hâ‚ : x â‰  0) (hâ‚‚ : âˆ€ â¦ƒw'â¦„, w' â‰  w â†’ w' x < 1) (hâ‚ƒ : IsReal w âˆ¨ |(w.embedding x).re| < 1) :
    Algebra.adjoin â„š {(x : K)} = âŠ¤ := by
  rw [â† IntermediateField.adjoin_simple_toSubalgebra_of_integral (IsIntegral.of_finite â„š _)]
  exact congr_arg IntermediateField.toSubalgebra <|
    NumberField.is_primitive_element_of_infinitePlace_lt hâ‚ hâ‚‚ hâ‚ƒ

open Fintype FiniteDimensional

variable (K)

/-- The number of infinite real places of the number field `K`. -/
noncomputable abbrev NrRealPlaces := card { w : InfinitePlace K // IsReal w }

/-- The number of infinite complex places of the number field `K`. -/
noncomputable abbrev NrComplexPlaces := card { w : InfinitePlace K // IsComplex w }

theorem card_real_embeddings :
    card { Ï† : K â†’+* â„‚ // ComplexEmbedding.IsReal Ï† } = NrRealPlaces K := Fintype.card_congr mkReal
#align number_field.infinite_place.card_real_embeddings NumberField.InfinitePlace.card_real_embeddings

theorem card_eq_nrRealPlaces_add_nrComplexPlaces :
    Fintype.card (InfinitePlace K) = NrRealPlaces K + NrComplexPlaces K := by
  convert Fintype.card_subtype_or_disjoint (IsReal (K := K)) (IsComplex (K := K))
    (disjoint_isReal_isComplex K) using 1
  exact (Fintype.card_of_subtype _ (fun w â†¦ âŸ¨fun _ â†¦ isReal_or_isComplex w, fun _ â†¦ by simpâŸ©)).symm

theorem card_complex_embeddings :
    card { Ï† : K â†’+* â„‚ // Â¬ComplexEmbedding.IsReal Ï† } = 2 * NrComplexPlaces K := by
  suffices âˆ€ w : { w : InfinitePlace K // IsComplex w }, (Finset.univ.filter
      fun Ï† : { Ï† // Â¬ ComplexEmbedding.IsReal Ï† } => mkComplex Ï† = w).card = 2 by
    rw [Fintype.card, Finset.card_eq_sum_ones, â† Finset.sum_fiberwise _ (fun Ï† => mkComplex Ï†)]
    simp_rw [Finset.sum_const, this, smul_eq_mul, mul_one, Fintype.card, Finset.card_eq_sum_ones,
      Finset.mul_sum, Finset.sum_const, smul_eq_mul, mul_one]
  rintro âŸ¨w, hwâŸ©
  convert card_filter_mk_eq w
  Â· rw [â† Fintype.card_subtype, â† Fintype.card_subtype]
    refine Fintype.card_congr (Equiv.ofBijective ?_ âŸ¨fun _ _ h => ?_, fun âŸ¨Ï†, hÏ†âŸ© => ?_âŸ©)
    Â· exact fun âŸ¨Ï†, hÏ†âŸ© => âŸ¨Ï†.val, by rwa [Subtype.ext_iff] at hÏ†âŸ©
    Â· rwa [Subtype.mk_eq_mk, â† Subtype.ext_iff, â† Subtype.ext_iff] at h
    Â· refine âŸ¨âŸ¨âŸ¨Ï†, not_isReal_of_mk_isComplex (hÏ†.symm â–¸ hw)âŸ©, ?_âŸ©, rflâŸ©
      rwa [Subtype.ext_iff, mkComplex_coe]
  Â· simp_rw [mult, not_isReal_iff_isComplex.mpr hw, ite_false]
#align number_field.infinite_place.card_complex_embeddings NumberField.InfinitePlace.card_complex_embeddings

theorem card_add_two_mul_card_eq_rank :
    NrRealPlaces K + 2 * NrComplexPlaces K = finrank â„š K := by
  rw [â† card_real_embeddings, â† card_complex_embeddings, Fintype.card_subtype_compl,
    â† Embeddings.card K â„‚, Nat.add_sub_of_le]
  exact Fintype.card_subtype_le _

variable {K}

theorem nrComplexPlaces_eq_zero_of_finrank_eq_one (h : finrank â„š K = 1) :
    NrComplexPlaces K = 0 := by linarith [card_add_two_mul_card_eq_rank K]

theorem nrRealPlaces_eq_one_of_finrank_eq_one (h : finrank â„š K = 1) :
    NrRealPlaces K = 1 := by
  have := card_add_two_mul_card_eq_rank K
  rwa [nrComplexPlaces_eq_zero_of_finrank_eq_one h, h, mul_zero, add_zero] at this

/-- The restriction of an infinite place along an embedding. -/
def comap (w : InfinitePlace K) (f : k â†’+* K) : InfinitePlace k :=
  âŸ¨w.1.comp f.injective, w.embedding.comp f,
    by { ext x; show _ = w.1 (f x); rw [â† w.2.choose_spec]; rfl }âŸ©

@[simp]
lemma comap_mk (Ï† : K â†’+* â„‚) (f : k â†’+* K) : (mk Ï†).comap f = mk (Ï†.comp f) := rfl

lemma comap_id (w : InfinitePlace K) : w.comap (RingHom.id K) = w := rfl

lemma comap_comp (w : InfinitePlace K) (f : F â†’+* K) (g : k â†’+* F) :
    w.comap (f.comp g) = (w.comap f).comap g := rfl

lemma IsReal.comap (f : k â†’+* K) {w : InfinitePlace K} (hÏ† : IsReal w) :
    IsReal (w.comap f) := by
  rw [â† mk_embedding w, comap_mk, isReal_mk_iff]
  rw [â† mk_embedding w, isReal_mk_iff] at hÏ†
  exact hÏ†.comp f

lemma isReal_comap_iff (f : k â‰ƒ+* K) {w : InfinitePlace K} :
    IsReal (w.comap (f : k â†’+* K)) â†” IsReal w := by
  rw [â† mk_embedding w, comap_mk, isReal_mk_iff, isReal_mk_iff, ComplexEmbedding.isReal_comp_iff]

lemma comap_surjective [Algebra k K] [Algebra.IsAlgebraic k K] :
    Function.Surjective (comap Â· (algebraMap k K)) := fun w â†¦
  letI := w.embedding.toAlgebra
  âŸ¨mk (IsAlgClosed.lift (M := â„‚) (R := k)).toRingHom,
    by simp [comap_mk, RingHom.algebraMap_toAlgebra]âŸ©

lemma mult_comap_le (f : k â†’+* K) (w : InfinitePlace K) : mult (w.comap f) â‰¤ mult w := by
  rw [mult, mult]
  split_ifs with hâ‚ hâ‚‚ hâ‚‚
  pick_goal 3
  Â· exact (hâ‚ (hâ‚‚.comap _)).elim
  all_goals decide

variable [Algebra k K] [Algebra k F] [Algebra K F] [IsScalarTower k K F]
variable (Ïƒ : K â‰ƒâ‚[k] K) (w : InfinitePlace K)
variable (k K)

lemma card_mono [NumberField k] [NumberField K] :
    card (InfinitePlace k) â‰¤ card (InfinitePlace K) :=
  have := Module.Finite.of_restrictScalars_finite â„š k K
  Fintype.card_le_of_surjective _ comap_surjective

variable {k K}

/-- The action of the galois group on infinite places. -/
@[simps! smul_coe_apply]
instance : MulAction (K â‰ƒâ‚[k] K) (InfinitePlace K) where
  smul := fun Ïƒ w â†¦ w.comap Ïƒ.symm
  one_smul := fun _ â†¦ rfl
  mul_smul := fun _ _ _ â†¦ rfl

lemma smul_eq_comap : Ïƒ â€¢ w = w.comap Ïƒ.symm := rfl

@[simp] lemma smul_apply (x) : (Ïƒ â€¢ w) x = w (Ïƒ.symm x) := rfl

@[simp] lemma smul_mk (Ï† : K â†’+* â„‚) : Ïƒ â€¢ mk Ï† = mk (Ï†.comp Ïƒ.symm) := rfl

lemma comap_smul {f : F â†’+* K} : (Ïƒ â€¢ w).comap f = w.comap (RingHom.comp Ïƒ.symm f) := rfl

variable {Ïƒ w}

lemma isReal_smul_iff : IsReal (Ïƒ â€¢ w) â†” IsReal w := isReal_comap_iff (f := Ïƒ.symm.toRingEquiv)

lemma isComplex_smul_iff : IsComplex (Ïƒ â€¢ w) â†” IsComplex w := by
  rw [â† not_isReal_iff_isComplex, â† not_isReal_iff_isComplex, isReal_smul_iff]

lemma ComplexEmbedding.exists_comp_symm_eq_of_comp_eq [Algebra k K] [IsGalois k K] (Ï† Ïˆ : K â†’+* â„‚)
    (h : Ï†.comp (algebraMap k K) = Ïˆ.comp (algebraMap k K)) :
    âˆƒ Ïƒ : K â‰ƒâ‚[k] K, Ï†.comp Ïƒ.symm = Ïˆ := by
  letI := (Ï†.comp (algebraMap k K)).toAlgebra
  letI := Ï†.toAlgebra
  have : IsScalarTower k K â„‚ := IsScalarTower.of_algebraMap_eq' rfl
  let Ïˆ' : K â†’â‚[k] â„‚ := { Ïˆ with commutes' := fun r â†¦ (RingHom.congr_fun h r).symm }
  use (AlgHom.restrictNormal' Ïˆ' K).symm
  ext1 x
  exact AlgHom.restrictNormal_commutes Ïˆ' K x

lemma exists_smul_eq_of_comap_eq [Algebra k K] [IsGalois k K] {w w' : InfinitePlace K}
    (h : w.comap (algebraMap k K) = w'.comap (algebraMap k K)) : âˆƒ Ïƒ : K â‰ƒâ‚[k] K, Ïƒ â€¢ w = w' := by
  rw [â† mk_embedding w, â† mk_embedding w', comap_mk, comap_mk, mk_eq_iff] at h
  cases h with
  | inl h =>
    obtain âŸ¨Ïƒ, hÏƒâŸ© := ComplexEmbedding.exists_comp_symm_eq_of_comp_eq w.embedding w'.embedding h
    use Ïƒ
    rw [â† mk_embedding w, â† mk_embedding w', smul_mk, hÏƒ]
  | inr h =>
    obtain âŸ¨Ïƒ, hÏƒâŸ© := ComplexEmbedding.exists_comp_symm_eq_of_comp_eq
      ((starRingEnd â„‚).comp (embedding w)) w'.embedding h
    use Ïƒ
    rw [â† mk_embedding w, â† mk_embedding w', smul_mk, mk_eq_iff]
    exact Or.inr hÏƒ

lemma mem_orbit_iff [IsGalois k K] {w w' : InfinitePlace K} :
    w' âˆˆ MulAction.orbit (K â‰ƒâ‚[k] K) w â†” w.comap (algebraMap k K) = w'.comap (algebraMap k K) := by
  refine âŸ¨?_, exists_smul_eq_of_comap_eqâŸ©
  rintro âŸ¨Ïƒ, rfl : Ïƒ â€¢ w = w'âŸ©
  rw [â† mk_embedding w, comap_mk, smul_mk, comap_mk]
  congr 1; ext1; simp

/-- The orbits of infinite places under the action of the galois group are indexed by
the infinite places of the base field. -/
noncomputable
def orbitRelEquiv [IsGalois k K] :
    Quotient (MulAction.orbitRel (K â‰ƒâ‚[k] K) (InfinitePlace K)) â‰ƒ InfinitePlace k := by
  refine Equiv.ofBijective (Quotient.lift (comap Â· (algebraMap k K))
    fun _ _ e â†¦ (mem_orbit_iff.mp e).symm) âŸ¨?_, ?_âŸ©
  Â· rintro âŸ¨wâŸ© âŸ¨w'âŸ© e
    exact Quotient.sound (mem_orbit_iff.mpr e.symm)
  Â· intro w
    obtain âŸ¨w', hwâŸ© := comap_surjective (K := K) w
    exact âŸ¨âŸ¦w'âŸ§, hwâŸ©

lemma orbitRelEquiv_apply_mk'' [IsGalois k K] (w : InfinitePlace K) :
    orbitRelEquiv (Quotient.mk'' w) = comap w (algebraMap k K) := rfl

variable (k w)

/--
An infinite place is unramified in a field extension if the restriction has the same multiplicity.
-/
def IsUnramified : Prop := mult (w.comap (algebraMap k K)) = mult w

variable {k}

lemma isUnramified_self : IsUnramified K w := rfl

variable {w}

lemma IsUnramified.eq (h : IsUnramified k w) : mult (w.comap (algebraMap k K)) = mult w := h

lemma isUnramified_iff_mult_le :
    IsUnramified k w â†” mult w â‰¤ mult (w.comap (algebraMap k K)) := by
  rw [IsUnramified, le_antisymm_iff, and_iff_right]
  exact mult_comap_le _ _

lemma IsUnramified.comap_algHom {w : InfinitePlace F} (h : IsUnramified k w) (f : K â†’â‚[k] F) :
    IsUnramified k (w.comap (f : K â†’+* F)) := by
  rw [InfinitePlace.isUnramified_iff_mult_le, â† InfinitePlace.comap_comp, f.comp_algebraMap, h.eq]
  exact InfinitePlace.mult_comap_le _ _

variable (K)

lemma IsUnramified.of_restrictScalars {w : InfinitePlace F} (h : IsUnramified k w) :
    IsUnramified K w := by
  rw [InfinitePlace.isUnramified_iff_mult_le, â† h.eq, IsScalarTower.algebraMap_eq k K F,
    InfinitePlace.comap_comp]
  exact InfinitePlace.mult_comap_le _ _

lemma IsUnramified.comap {w : InfinitePlace F} (h : IsUnramified k w) :
    IsUnramified k (w.comap (algebraMap K F)) :=
  h.comap_algHom (IsScalarTower.toAlgHom k K F)

variable {K}

lemma not_isUnramified_iff :
    Â¬ IsUnramified k w â†” IsComplex w âˆ§ IsReal (w.comap (algebraMap k K)) := by
  rw [IsUnramified, mult, mult, â† not_isReal_iff_isComplex]
  split_ifs with hâ‚ hâ‚‚ hâ‚‚ <;>
    simp only [not_true_eq_false, false_iff, and_self, forall_true_left, IsEmpty.forall_iff,
      not_and, OfNat.one_ne_ofNat, not_false_eq_true, true_iff, OfNat.ofNat_ne_one, hâ‚, hâ‚‚]
  exact hâ‚ (hâ‚‚.comap _)

lemma isUnramified_iff :
    IsUnramified k w â†” IsReal w âˆ¨ IsComplex (w.comap (algebraMap k K)) := by
  rw [â† not_iff_not, not_isUnramified_iff, not_or,
    not_isReal_iff_isComplex, not_isComplex_iff_isReal]

variable (k)

lemma IsReal.isUnramified (h : IsReal w) : IsUnramified k w := isUnramified_iff.mpr (Or.inl h)

variable {k}

lemma _root_.NumberField.ComplexEmbedding.IsConj.isUnramified_mk_iff
    {Ï† : K â†’+* â„‚} (h : ComplexEmbedding.IsConj Ï† Ïƒ) :
    IsUnramified k (mk Ï†) â†” Ïƒ = 1 := by
  rw [h.ext_iff, ComplexEmbedding.isConj_one_iff, â† not_iff_not, not_isUnramified_iff,
    â† not_isReal_iff_isComplex, comap_mk, isReal_mk_iff, isReal_mk_iff, eq_true h.isReal_comp,
    and_true]

lemma isUnramified_mk_iff_forall_isConj [IsGalois k K] {Ï† : K â†’+* â„‚} :
    IsUnramified k (mk Ï†) â†” âˆ€ Ïƒ : K â‰ƒâ‚[k] K, ComplexEmbedding.IsConj Ï† Ïƒ â†’ Ïƒ = 1 := by
  refine âŸ¨fun H Ïƒ hÏƒ â†¦ hÏƒ.isUnramified_mk_iff.mp H,
    fun H â†¦ ?_âŸ©
  by_contra hÏ†
  rw [not_isUnramified_iff] at hÏ†
  rw [comap_mk, isReal_mk_iff, â† not_isReal_iff_isComplex, isReal_mk_iff,
    â† ComplexEmbedding.isConj_one_iff (k := k)] at hÏ†
  letI := (Ï†.comp (algebraMap k K)).toAlgebra
  letI := Ï†.toAlgebra
  have : IsScalarTower k K â„‚ := IsScalarTower.of_algebraMap_eq' rfl
  let Ï†' : K â†’â‚[k] â„‚ := { star Ï† with commutes' := fun r â†¦ by simpa using RingHom.congr_fun hÏ†.2 r }
  have : ComplexEmbedding.IsConj Ï† (AlgHom.restrictNormal' Ï†' K) :=
    (RingHom.ext <| AlgHom.restrictNormal_commutes Ï†' K).symm
  exact hÏ†.1 (H _ this â–¸ this)

local notation "Stab" => MulAction.stabilizer (K â‰ƒâ‚[k] K)

lemma mem_stabilizer_mk_iff (Ï† : K â†’+* â„‚) (Ïƒ : K â‰ƒâ‚[k] K) :
    Ïƒ âˆˆ Stab (mk Ï†) â†” Ïƒ = 1 âˆ¨ ComplexEmbedding.IsConj Ï† Ïƒ := by
  simp only [MulAction.mem_stabilizer_iff, smul_mk, mk_eq_iff]
  rw [â† ComplexEmbedding.isConj_symm, ComplexEmbedding.conjugate, star_eq_iff_star_eq]
  refine or_congr âŸ¨fun H â†¦ ?_, fun H â†¦ H â–¸ rflâŸ© Iff.rfl
  exact congr_arg AlgEquiv.symm
    (AlgEquiv.ext (g := AlgEquiv.refl) fun x â†¦ Ï†.injective (RingHom.congr_fun H x))

lemma IsUnramified.stabilizer_eq_bot (h : IsUnramified k w) : Stab w = âŠ¥ := by
  rw [eq_bot_iff, â† mk_embedding w, SetLike.le_def]
  simp only [mem_stabilizer_mk_iff, Subgroup.mem_bot, forall_eq_or_imp, true_and]
  exact fun Ïƒ hÏƒ â†¦ hÏƒ.isUnramified_mk_iff.mp ((mk_embedding w).symm â–¸ h)

lemma _root_.NumberField.ComplexEmbedding.IsConj.coe_stabilzer_mk
    {Ï† : K â†’+* â„‚} (h : ComplexEmbedding.IsConj Ï† Ïƒ) :
    (Stab (mk Ï†) : Set (K â‰ƒâ‚[k] K)) = {1, Ïƒ} := by
  ext
  rw [SetLike.mem_coe, mem_stabilizer_mk_iff, Set.mem_insert_iff, Set.mem_singleton_iff,
    â† h.ext_iff, eq_comm (a := Ïƒ)]

variable (k w)

lemma nat_card_stabilizer_eq_one_or_two :
    Nat.card (Stab w) = 1 âˆ¨ Nat.card (Stab w) = 2 := by
  rw [â† SetLike.coe_sort_coe, â† mk_embedding w]
  by_cases h : âˆƒ Ïƒ, ComplexEmbedding.IsConj (k := k) (embedding w) Ïƒ
  Â· obtain âŸ¨Ïƒ, hÏƒâŸ© := h
    simp only [hÏƒ.coe_stabilzer_mk, Nat.card_eq_fintype_card, card_ofFinset, Set.toFinset_singleton]
    by_cases 1 = Ïƒ
    Â· left; simp [*]
    Â· right; simp [*]
  Â· push_neg at h
    left
    trans Nat.card ({1} : Set (K â‰ƒâ‚[k] K))
    Â· congr with x
      simp only [SetLike.mem_coe, mem_stabilizer_mk_iff, Set.mem_singleton_iff, or_iff_left_iff_imp,
        h x, IsEmpty.forall_iff]
    Â· simp

variable {k w}

lemma isUnramified_iff_stabilizer_eq_bot [IsGalois k K] : IsUnramified k w â†” Stab w = âŠ¥ := by
  rw [â† mk_embedding w, isUnramified_mk_iff_forall_isConj]
  simp only [eq_bot_iff, SetLike.le_def, mem_stabilizer_mk_iff,
    Subgroup.mem_bot, forall_eq_or_imp, true_and]

lemma isUnramified_iff_card_stabilizer_eq_one [IsGalois k K] :
    IsUnramified k w â†” Nat.card (Stab w) = 1 := by
  rw [isUnramified_iff_stabilizer_eq_bot, Subgroup.card_eq_one]

lemma not_isUnramified_iff_card_stabilizer_eq_two [IsGalois k K] :
    Â¬ IsUnramified k w â†” Nat.card (Stab w) = 2 := by
  rw [isUnramified_iff_card_stabilizer_eq_one]
  obtain (e|e) := nat_card_stabilizer_eq_one_or_two k w <;> rw [e] <;> decide

lemma card_stabilizer [IsGalois k K] :
    Nat.card (Stab w) = if IsUnramified k w then 1 else 2 := by
  split
  Â· rwa [â† isUnramified_iff_card_stabilizer_eq_one]
  Â· rwa [â† not_isUnramified_iff_card_stabilizer_eq_two]

lemma even_nat_card_aut_of_not_isUnramified [IsGalois k K] (hw : Â¬ IsUnramified k w) :
    Even (Nat.card <| K â‰ƒâ‚[k] K) := by
  by_cases H : Finite (K â‰ƒâ‚[k] K)
  Â· cases nonempty_fintype (K â‰ƒâ‚[k] K)
    rw [even_iff_two_dvd, Nat.card_eq_fintype_card,
      â† not_isUnramified_iff_card_stabilizer_eq_two.mp hw, Nat.card_eq_fintype_card]
    exact Subgroup.card_subgroup_dvd_card (Stab w)
  Â· convert even_zero
    by_contra e
    exact H (Nat.finite_of_card_ne_zero e)

lemma even_card_aut_of_not_isUnramified [IsGalois k K] [FiniteDimensional k K]
    (hw : Â¬ IsUnramified k w) :
    Even (Fintype.card <| K â‰ƒâ‚[k] K) :=
  Nat.card_eq_fintype_card (Î± := K â‰ƒâ‚[k] K) â–¸ even_nat_card_aut_of_not_isUnramified hw

lemma even_finrank_of_not_isUnramified [IsGalois k K]
    (hw : Â¬ IsUnramified k w) : Even (finrank k K) := by
  by_cases FiniteDimensional k K
  Â· exact IsGalois.card_aut_eq_finrank k K â–¸ even_card_aut_of_not_isUnramified hw
  Â· exact finrank_of_not_finite â€¹_â€º â–¸ even_zero

lemma isUnramified_smul_iff :
    IsUnramified k (Ïƒ â€¢ w) â†” IsUnramified k w := by
  rw [isUnramified_iff, isUnramified_iff, isReal_smul_iff, comap_smul,
    â† AlgEquiv.toAlgHom_toRingHom, AlgHom.comp_algebraMap]

variable (K)

/-- A infinite place of the base field is unramified in a field extension if every
infinite place over it is unramified. -/
def IsUnramifiedIn (w : InfinitePlace k) : Prop :=
  âˆ€ v, comap v (algebraMap k K) = w â†’ IsUnramified k v

variable {K}

lemma isUnramifiedIn_comap [IsGalois k K] {w : InfinitePlace K} :
    (w.comap (algebraMap k K)).IsUnramifiedIn K â†” w.IsUnramified k := by
  refine âŸ¨fun H â†¦ H _ rfl, fun H v hv â†¦ ?_âŸ©
  obtain âŸ¨Ïƒ, rflâŸ© := exists_smul_eq_of_comap_eq hv
  rwa [isUnramified_smul_iff] at H

lemma even_card_aut_of_not_isUnramifiedIn [IsGalois k K] [FiniteDimensional k K]
    {w : InfinitePlace k} (hw : Â¬ w.IsUnramifiedIn K) :
    Even (Fintype.card <| K â‰ƒâ‚[k] K) := by
  obtain âŸ¨v, rflâŸ© := comap_surjective (K := K) w
  rw [isUnramifiedIn_comap] at hw
  exact even_card_aut_of_not_isUnramified hw

lemma even_finrank_of_not_isUnramifiedIn
    [IsGalois k K] {w : InfinitePlace k} (hw : Â¬ w.IsUnramifiedIn K) :
    Even (finrank k K) := by
  obtain âŸ¨v, rflâŸ© := comap_surjective (K := K) w
  rw [isUnramifiedIn_comap] at hw
  exact even_finrank_of_not_isUnramified hw

variable (k K)

open Finset in
lemma card_isUnramified [NumberField k] [IsGalois k K] :
    Finset.card (univ.filter <| IsUnramified k (K := K)) =
      Finset.card (univ.filter <| IsUnramifiedIn K (k := k)) * (finrank k K) := by
  letI := Module.Finite.of_restrictScalars_finite â„š k K
  rw [â† IsGalois.card_aut_eq_finrank,
    Finset.card_eq_sum_card_fiberwise (f := (comap Â· (algebraMap k K)))
    (t := (univ.filter <| IsUnramifiedIn K (k := k))), â† smul_eq_mul, â† sum_const]
  Â· refine sum_congr rfl (fun w hw â†¦ ?_)
    obtain âŸ¨w, rflâŸ© := comap_surjective (K := K) w
    simp only [mem_univ, forall_true_left, mem_filter, true_and] at hw
    trans Finset.card (MulAction.orbit (K â‰ƒâ‚[k] K) w).toFinset
    Â· congr; ext w'
      simp only [mem_univ, forall_true_left, filter_congr_decidable, mem_filter, true_and,
        Set.mem_toFinset, mem_orbit_iff, @eq_comm _ (comap w' _), and_iff_right_iff_imp]
      intro e; rwa [â† isUnramifiedIn_comap, â† e]
    Â· rw [â† MulAction.card_orbit_mul_card_stabilizer_eq_card_group _ w,
        â† Nat.card_eq_fintype_card (Î± := Stab w), card_stabilizer, if_pos,
        mul_one, Set.toFinset_card]
      rwa [â† isUnramifiedIn_comap]
  Â· simp [isUnramifiedIn_comap]

open Finset in
lemma card_isUnramified_compl [NumberField k] [IsGalois k K] :
    Finset.card (univ.filter <| IsUnramified k (K := K))á¶œ =
      Finset.card (univ.filter <| IsUnramifiedIn K (k := k))á¶œ * (finrank k K / 2) := by
  letI := Module.Finite.of_restrictScalars_finite â„š k K
  rw [â† IsGalois.card_aut_eq_finrank,
    Finset.card_eq_sum_card_fiberwise (f := (comap Â· (algebraMap k K)))
    (t := (univ.filter <| IsUnramifiedIn K (k := k))á¶œ), â† smul_eq_mul, â† sum_const]
  Â· refine sum_congr rfl (fun w hw â†¦ ?_)
    obtain âŸ¨w, rflâŸ© := comap_surjective (K := K) w
    simp only [mem_univ, forall_true_left, compl_filter, not_not, mem_filter, true_and] at hw
    trans Finset.card (MulAction.orbit (K â‰ƒâ‚[k] K) w).toFinset
    Â· congr; ext w'
      simp only [compl_filter, filter_congr_decidable, mem_filter, mem_univ, true_and,
        @eq_comm _ (comap w' _), Set.mem_toFinset, mem_orbit_iff, and_iff_right_iff_imp]
      intro e; rwa [â† isUnramifiedIn_comap, â† e]
    Â· rw [â† MulAction.card_orbit_mul_card_stabilizer_eq_card_group _ w,
        â† Nat.card_eq_fintype_card (Î± := Stab w), InfinitePlace.card_stabilizer, if_neg,
        Nat.mul_div_cancel _ zero_lt_two, Set.toFinset_card]
      rwa [â† isUnramifiedIn_comap]
  Â· simp [isUnramifiedIn_comap]

lemma card_eq_card_isUnramifiedIn [NumberField k] [IsGalois k K] :
    Fintype.card (InfinitePlace K) =
      Finset.card (Finset.univ.filter <| IsUnramifiedIn K (k := k)) * finrank k K +
      Finset.card (Finset.univ.filter <| IsUnramifiedIn K (k := k))á¶œ * (finrank k K / 2) := by
  rw [â† card_isUnramified, â† card_isUnramified_compl, Finset.card_add_card_compl]

end NumberField.InfinitePlace

variable (k K F)
variable [Algebra k K] [Algebra k F] [Algebra K F] [IsScalarTower k K F]

/-- A field extension is unramified at infinite places if every infinite place is unramified. -/
class IsUnramifiedAtInfinitePlaces : Prop where
  isUnramified : âˆ€ w : InfinitePlace K, w.IsUnramified k

instance IsUnramifiedAtInfinitePlaces.id : IsUnramifiedAtInfinitePlaces K K where
  isUnramified w := w.isUnramified_self

lemma IsUnramifiedAtInfinitePlaces.trans
    [hâ‚ : IsUnramifiedAtInfinitePlaces k K] [hâ‚‚ : IsUnramifiedAtInfinitePlaces K F] :
    IsUnramifiedAtInfinitePlaces k F where
  isUnramified w :=
    Eq.trans (IsScalarTower.algebraMap_eq k K F â–¸ hâ‚.1 (w.comap (algebraMap _ _))) (hâ‚‚.1 w)

lemma IsUnramifiedAtInfinitePlaces.top [h : IsUnramifiedAtInfinitePlaces k F] :
    IsUnramifiedAtInfinitePlaces K F where
  isUnramified w := (h.1 w).of_restrictScalars K

lemma IsUnramifiedAtInfinitePlaces.bot [hâ‚ : IsUnramifiedAtInfinitePlaces k F]
    [Algebra.IsAlgebraic K F] :
    IsUnramifiedAtInfinitePlaces k K where
  isUnramified w := by
    obtain âŸ¨w, rflâŸ© := InfinitePlace.comap_surjective (K := F) w
    exact (hâ‚.1 w).comap K

variable {K}

lemma NumberField.InfinitePlace.isUnramified [IsUnramifiedAtInfinitePlaces k K]
    (w : InfinitePlace K) : IsUnramified k w := IsUnramifiedAtInfinitePlaces.isUnramified w

variable {k} (K)

lemma NumberField.InfinitePlace.isUnramifiedIn [IsUnramifiedAtInfinitePlaces k K]
    (w : InfinitePlace k) : IsUnramifiedIn K w := fun v _ â†¦ v.isUnramified k

variable {K}

lemma IsUnramifiedAtInfinitePlaces_of_odd_card_aut [IsGalois k K] [FiniteDimensional k K]
    (h : Odd (Fintype.card <| K â‰ƒâ‚[k] K)) : IsUnramifiedAtInfinitePlaces k K :=
  âŸ¨fun _ â†¦ not_not.mp (Nat.odd_iff_not_even.mp h âˆ˜ InfinitePlace.even_card_aut_of_not_isUnramified)âŸ©

lemma IsUnramifiedAtInfinitePlaces_of_odd_finrank [IsGalois k K]
    (h : Odd (FiniteDimensional.finrank k K)) : IsUnramifiedAtInfinitePlaces k K :=
  âŸ¨fun _ â†¦ not_not.mp (Nat.odd_iff_not_even.mp h âˆ˜ InfinitePlace.even_finrank_of_not_isUnramified)âŸ©

variable (k K)

open FiniteDimensional in
lemma IsUnramifiedAtInfinitePlaces.card_infinitePlace [NumberField k] [NumberField K]
    [IsGalois k K] [IsUnramifiedAtInfinitePlaces k K] :
    Fintype.card (InfinitePlace K) = Fintype.card (InfinitePlace k) * finrank k K := by
  rw [InfinitePlace.card_eq_card_isUnramifiedIn (k := k) (K := K), Finset.filter_true_of_mem,
    Finset.card_univ, Finset.card_eq_zero.mpr, zero_mul, add_zero]
  Â· exact Finset.compl_univ
  simp only [Finset.mem_univ, forall_true_left, Finset.filter_eq_empty_iff]
  exact InfinitePlace.isUnramifiedIn K

end InfinitePlace

namespace IsPrimitiveRoot

variable {K : Type*} [Field K] [NumberField K] {Î¶ : K} {k : â„•}

theorem nrRealPlaces_eq_zero_of_two_lt (hk : 2 < k) (hÎ¶ : IsPrimitiveRoot Î¶ k) :
    NumberField.InfinitePlace.NrRealPlaces K = 0 := by
  refine (@Fintype.card_eq_zero_iff _ (_)).2 âŸ¨fun âŸ¨w, hwrealâŸ© â†¦ ?_âŸ©
  rw [NumberField.InfinitePlace.isReal_iff] at hwreal
  let f := w.embedding
  have hÎ¶' : IsPrimitiveRoot (f Î¶) k := hÎ¶.map_of_injective f.injective
  have him : (f Î¶).im = 0 := by
    rw [â† Complex.conj_eq_iff_im, â† NumberField.ComplexEmbedding.conjugate_coe_eq]
    congr
  have hre : (f Î¶).re = 1 âˆ¨ (f Î¶).re = -1 := by
    rw [â† Complex.abs_re_eq_abs] at him
    have := Complex.norm_eq_one_of_pow_eq_one hÎ¶'.pow_eq_one (by linarith)
    rwa [Complex.norm_eq_abs, â† him, â† abs_one, abs_eq_abs] at this
  cases hre with
  | inl hone =>
    exact hÎ¶'.ne_one (by linarith) <| Complex.ext (by simp [hone]) (by simp [him])
  | inr hnegone =>
    replace hÎ¶' := hÎ¶'.eq_orderOf
    simp only [show f Î¶ = -1 from Complex.ext (by simp [hnegone]) (by simp [him]),
      orderOf_neg_one, ringChar.eq_zero, OfNat.zero_ne_ofNat, â†“reduceIte] at hÎ¶'
    linarith

end IsPrimitiveRoot
