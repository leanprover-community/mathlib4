/-
Copyright (c) 2024 Fabrizio Barroero. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fabrizio Barroero
-/
import Mathlib.NumberTheory.NumberField.FinitePlaces

/-!
# The Product Formula for number fields
In this file we prove the Product Formula for number fields: for any non-zero element `x` of a
number field `K`, we have `âˆ|x|áµ¥=1` where the product runs over the equivalence classes of absoulte
values of `K` and the `|â¬|áµ¥` are suitably normalized.

## Main Results
* `NumberField.FinitePlace.prod_eq_inv_abs_norm`: for any non-zero element `x` of a number field
`K`, the product `âˆ|x|áµ¥` of the absolute values of `x` associated to the finite places of `K` is
equal to the inverse of the norm of `x`.
* `NumberField.prod_abs_eq_one`: for any non-zero element `x` of a number field `K`, we have
`âˆ|x|áµ¥=1` where the product runs over the equivalence classes of absoulte values of `K`.

## Tags
number field, embeddings, places, infinite places, finite places, product formula
-/

namespace NumberField

variable {K : Type*} [Field K] [NumberField K]

open Algebra Classical FinitePlace Function Ideal IsDedekindDomain
  IsDedekindDomain.HeightOneSpectrum

/-- For any non-zero `x` in `ğ“ K`, the prduct of `w x`, where `w` runs over `FinitePlace K`, is
equal to the inverse of the absolute value of `Algebra.norm â„¤ x`. -/
theorem FinitePlace.prod_eq_inv_abs_norm_int {x : ğ“ K} (h_x_nezero : x â‰  0) :
    âˆá¶  w : FinitePlace K, w x = (|norm â„¤ x| : â„)â»Â¹ := by
  convert_to âˆá¶  v : HeightOneSpectrum (ğ“ K), â€–embedding v xâ€– = |â†‘(norm â„¤ x)|â»Â¹
  Â· exact (finprod_eq_of_bijective maximalIdeal ((bijective_iff_existsUnique _).mpr
      <| fun v â†¦ âŸ¨mk v, maximalIdeal_mk v, fun _ a â†¦ by rw [â† a, mk_maximalIdeal]âŸ©)
      (fun w â†¦ (norm_embedding_eq w (x : K)).symm))
  apply (inv_eq_of_mul_eq_one_left _).symm
  norm_cast
  have h_span_nezero : span {x} â‰  0 := by
    simp only [Submodule.zero_eq_bot, ne_eq, span_singleton_eq_bot, h_x_nezero, not_false_eq_true]
  rw [Int.abs_eq_natAbs, â† Ideal.absNorm_span_singleton,
    â† Ideal.finprod_heightOneSpectrum_factorization h_span_nezero, Int.cast_natCast]
  --Aim: transform the two finprod into Finset.prod
  let tâ‚€ := {v : HeightOneSpectrum (ğ“ K) | x âˆˆ v.asIdeal}
  have h_finâ‚€ : tâ‚€.Finite := by
    simp only [â† dvd_span_singleton, finite_factors h_span_nezero, tâ‚€]
  let s := h_finâ‚€.toFinset
  let tâ‚ := (fun v : HeightOneSpectrum (ğ“ K) â†¦ â€–(embedding v) xâ€–).mulSupport
  let tâ‚‚ := (fun v : HeightOneSpectrum (ğ“ K) â†¦ v.maxPowDividing (span {x})).mulSupport
  have h_subsâ‚ : tâ‚ âŠ† tâ‚€ := by
    simp only [mulSupport_subset_iff, ne_eq, norm_eq_one_iff_not_mem, Decidable.not_not,
    Set.mem_setOf_eq, imp_self, implies_true, tâ‚, tâ‚€]
  have h_subsâ‚‚ : tâ‚‚ âŠ† tâ‚€ := by
    simp only [Set.le_eq_subset, mulSupport_subset_iff, Set.mem_setOf_eq, tâ‚‚, tâ‚€,
      maxPowDividing, â† dvd_span_singleton]
    intro v hv
    rw [â† pow_zero v.asIdeal] at hv
    replace hv := fun h â†¦ hv (congrArg (HPow.hPow v.asIdeal) h)
    rwa [imp_false, â† ne_eq, Associates.count_ne_zero_iff_dvd h_span_nezero (irreducible v)] at hv
  have h_finâ‚ : tâ‚.Finite := h_finâ‚€.subset h_subsâ‚
  have h_finâ‚‚ : tâ‚‚.Finite := h_finâ‚€.subset h_subsâ‚‚
  have h_subâ‚ : h_finâ‚.toFinset âŠ† s := Set.Finite.toFinset_subset_toFinset.mpr h_subsâ‚
  have h_subâ‚‚ : h_finâ‚‚.toFinset âŠ† s := Set.Finite.toFinset_subset_toFinset.mpr h_subsâ‚‚
  rw [finprod_eq_prod_of_mulSupport_toFinset_subset _ h_finâ‚ h_subâ‚,
    finprod_eq_prod_of_mulSupport_toFinset_subset _ h_finâ‚‚ h_subâ‚‚, map_prod, Nat.cast_prod,
    â† Finset.prod_mul_distrib, Finset.prod_eq_one]
  intro v _
  rw [maxPowDividing, map_pow, Nat.cast_pow, norm_def, vadicAbv_def,
    WithZeroMulInt.toNNReal_neg_apply _ (v.valuation.ne_zero_iff.mpr
    (RingOfIntegers.coe_ne_zero_iff.mpr h_x_nezero))]
  push_cast
  rw [â† Real.rpow_natCast, â† Real.rpow_intCast, â† Real.rpow_add (mod_cast Nat.zero_lt_of_lt
    (mod_cast one_lt_norm v))]
  norm_cast
  rw [zpow_eq_one_iff_rightâ‚€ (Nat.cast_nonneg' (Nat.card (ğ“ K â§¸ v.asIdeal)))
    (by exact ne_of_gt (one_lt_norm v))]
  simp only [valuation_eq_intValuationDef v x, intValuationDef_if_neg v h_x_nezero,
    WithZero.unzero_coe, toAdd_ofAdd, neg_add_cancel]

/-- For any non-zero `x` in `K`, the prduct of `w x`, where `w` runs over `FinitePlace K`, is
equal to the inverse of the absolute value of `Algebra.norm â„š x`. -/
theorem FinitePlace.prod_eq_inv_abs_norm {x : K} (h_x_nezero : x â‰  0) :
    âˆá¶  w : FinitePlace K, w x = |(Algebra.norm â„š) x|â»Â¹ := by
  --reduce to ğ“ K
  rcases IsFractionRing.div_surjective (A := ğ“ K) x with âŸ¨a, b, hb, rflâŸ©
  apply nonZeroDivisors.ne_zero at hb
  have ha : a â‰  0 := by
    by_contra! ha
    apply h_x_nezero
    simp only [ha, map_zero, zero_div]
  simp_rw [map_divâ‚€, Rat.cast_inv, Rat.cast_abs, finprod_div_distrib (mulSupport_finite_int ha)
    (mulSupport_finite_int hb), prod_eq_inv_abs_norm_int ha, prod_eq_inv_abs_norm_int hb]
  rw [â† inv_eq_iff_eq_inv, div_inv_eq_mul, mul_inv_rev, inv_inv, inv_mul_eq_div, â† abs_div]
  congr
  apply (eq_div_of_mul_eq (by simp only [ne_eq, Int.cast_eq_zero, norm_eq_zero_iff, hb,
    not_false_eq_true]) ?_).symm
  norm_cast
  rw [coe_norm_int a, coe_norm_int b, â† MonoidHom.map_mul, div_mul_cancelâ‚€ _
    (RingOfIntegers.coe_ne_zero_iff.mpr hb)]

/-- The Product Formula for the Number Field `K`. -/
theorem prod_abs_eq_one {x : K} (h_x_nezero : x â‰  0) :
    (âˆ w : InfinitePlace K, w x ^ w.mult) * âˆá¶  w : FinitePlace K, w x = 1 := by
  simp_all only [prod_eq_inv_abs_norm h_x_nezero, InfinitePlace.prod_eq_abs_norm, ne_eq,
    Rat.cast_inv, isUnit_iff_ne_zero, abs_eq_zero, Rat.cast_eq_zero, norm_eq_zero_iff,
    not_false_eq_true, IsUnit.mul_inv_cancel]

end NumberField
