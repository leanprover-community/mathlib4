/-
Copyright (c) 2024 Fabrizio Barroero. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fabrizio Barroero
-/
import Mathlib.NumberTheory.NumberField.FinitePlaces
import Mathlib.NumberTheory.NumberField.InfinitePlace.Basic

/-!
# The Product Formula for number fields

In this file we prove the Product Formula for number fields: for any non-zero element `x` of a
number field `K`, we have `âˆ |x|áµ¥=1` where the product runs over the equivalence classes of absolute
values of `K`. The `|Â·|áµ¥` are normalized as follows:
- for the infinite places, `|Â·|áµ¥` is the absolute value on `K` induced by the corresponding field
embedding in `â„‚` and the usual absolute value on `â„‚`;
- for the finite places and a non-zero `x`, `|x|áµ¥` is equal to the norm of the corresponding maximal
ideal of `ğ“ K` raised to the power of the `v`-adic valuation of `x`.

## Main Results

* `NumberField.FinitePlace.prod_eq_inv_abs_norm`: for any non-zero element `x` of a number field
`K`, the product `âˆ |x|áµ¥` of the absolute values of `x` associated to the finite places of `K` is
equal to the inverse of the norm of `x`.
* `NumberField.prod_abs_eq_one`: for any non-zero element `x` of a number field `K`, we have
`âˆ |x|áµ¥=1`, where the product runs over the equivalence classes of absolute values of `K`.

## Tags
number field, embeddings, places, infinite places, finite places, product formula
-/

namespace NumberField

variable {K : Type*} [Field K] [NumberField K]

open Algebra

open Function Ideal IsDedekindDomain HeightOneSpectrum in
/-- For any non-zero `x` in `ğ“ K`, the prduct of `w x`, where `w` runs over `FinitePlace K`, is
equal to the inverse of the absolute value of `Algebra.norm â„¤ x`. -/
theorem FinitePlace.prod_eq_inv_abs_norm_int {x : ğ“ K} (h_x_nezero : x â‰  0) :
    âˆá¶  w : FinitePlace K, w x = (|norm â„¤ x| : â„)â»Â¹ := by
  simp only [â† finprod_comp_equiv equivHeightOneSpectrum.symm, equivHeightOneSpectrum_symm_apply]
  refine (inv_eq_of_mul_eq_one_left ?_).symm
  norm_cast
  have h_span_nezero : span {x} â‰  0 := by simp [h_x_nezero]
  rw [Int.abs_eq_natAbs, â† absNorm_span_singleton,
    â† finprod_heightOneSpectrum_factorization h_span_nezero, Int.cast_natCast]
  let tâ‚€ := {v : HeightOneSpectrum (ğ“ K) | x âˆˆ v.asIdeal}
  have h_finâ‚€ : tâ‚€.Finite := by simp only [â† dvd_span_singleton, finite_factors h_span_nezero, tâ‚€]
  let tâ‚ := (fun v : HeightOneSpectrum (ğ“ K) â†¦ â€–embedding v (x : K)â€–).mulSupport
  let tâ‚‚ :=
    (fun v : HeightOneSpectrum (ğ“ K) â†¦ (absNorm (v.maxPowDividing (span {x})) : â„)).mulSupport
  have h_finâ‚ : tâ‚.Finite := h_finâ‚€.subset <| by simp [norm_eq_one_iff_notMem, tâ‚, tâ‚€]
  have h_finâ‚‚ : tâ‚‚.Finite := by
    refine h_finâ‚€.subset ?_
    simp only [mulSupport_subset_iff, Set.mem_setOf_eq, tâ‚‚, tâ‚€,
      maxPowDividing, â† dvd_span_singleton]
    intro v hv
    simp only [map_pow, Nat.cast_pow, â† pow_zero (absNorm v.asIdeal : â„)] at hv
    classical
    refine (Associates.count_ne_zero_iff_dvd h_span_nezero (irreducible v)).1 <| fun h â†¦ hv ?_
    congr
  have h_prod : (absNorm (âˆá¶  (v : HeightOneSpectrum (ğ“ K)), v.maxPowDividing (span {x})) : â„) =
      âˆá¶  (v : HeightOneSpectrum (ğ“ K)), (absNorm (v.maxPowDividing (span {x})) : â„) :=
    ((Nat.castRingHom â„).toMonoidHom.comp absNorm.toMonoidHom).map_finprod_of_preimage_one
      (by simp) _
  rw [h_prod, â† finprod_mul_distrib h_finâ‚ h_finâ‚‚]
  exact finprod_eq_one_of_forall_eq_one fun v â†¦ v.embedding_mul_absNorm h_x_nezero

/-- For any non-zero `x` in `K`, the prduct of `w x`, where `w` runs over `FinitePlace K`, is
equal to the inverse of the absolute value of `Algebra.norm â„š x`. -/
theorem FinitePlace.prod_eq_inv_abs_norm {x : K} (h_x_nezero : x â‰  0) :
    âˆá¶  w : FinitePlace K, w x = |(Algebra.norm â„š) x|â»Â¹ := by
  --reduce to ğ“ K
  rcases IsFractionRing.div_surjective (A := ğ“ K) x with âŸ¨a, b, hb, rflâŸ©
  apply nonZeroDivisors.ne_zero at hb
  have ha : a â‰  0 := by
    rintro rfl
    simp at h_x_nezero
  simp_rw [map_divâ‚€, Rat.cast_inv, Rat.cast_abs, finprod_div_distrib (mulSupport_finite_int ha)
    (mulSupport_finite_int hb), prod_eq_inv_abs_norm_int ha, prod_eq_inv_abs_norm_int hb]
  rw [â† inv_eq_iff_eq_inv, inv_inv_div_inv, â† abs_div]
  congr
  have hbâ‚€ : ((Algebra.norm â„¤) b : â„) â‰  0 := by simp [hb]
  refine (eq_div_of_mul_eq hbâ‚€ ?_).symm
  norm_cast
  rw [coe_norm_int a, coe_norm_int b, â† MonoidHom.map_mul, div_mul_cancelâ‚€ _
    (RingOfIntegers.coe_ne_zero_iff.mpr hb)]

open FinitePlace in
/-- The Product Formula for the Number Field `K`. -/
theorem prod_abs_eq_one {x : K} (h_x_nezero : x â‰  0) :
    (âˆ w : InfinitePlace K, w x ^ w.mult) * âˆá¶  w : FinitePlace K, w x = 1 := by
  simp [prod_eq_inv_abs_norm, InfinitePlace.prod_eq_abs_norm, h_x_nezero]

end NumberField
