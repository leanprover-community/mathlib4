/-
Copyright (c) 2025 X. Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.NumberTheory.NumberField.InfinitePlace.TotallyRealComplex
import Mathlib.NumberTheory.NumberField.Units.DirichletTheorem
import Mathlib.RingTheory.RootsOfUnity.Complex

/-!
# CM-extension of number fields

A CM-extension `K/F` of number fields is an extension where `K` is totally complex, `F` is
totally real and `K` is a quadratic extension of `F`. In this situation, the totally real
subfield `F` is (isomorphic to) the maximal real subfield `K‚Å∫` of `K`.

## Main definitions

* `NumberField.IsCMField`: A predicate that says that if a number field is CM, then it is a totally
  complex quadratic extension of its totally real subfield

* `NumberField.CMExtension.equivMaximalRealSubfield`: Any field `F` such that `K/F` is a
  CM-extension is isomorphic to the maximal real subfield `K‚Å∫` of `K`.

* `NumberField.IsCM.ofIsCMExtension`: Assume that there exists `F` such that `K/F` is a
  CM-extension. Then `K` is CM.

* `NumberField.IsCMField.of_isMulCommutative`: A totally complex abelian extension of `‚Ñö` is CM.

## Implementation note

Most results are proved for the case of a CM field, that is `K` is totally complex quadratic
extension of its totally real. These results live in the `NumberField.IsCMField` namespace. Some
results deal with the general case `K/F`, where `K` is totally complex, `F` is totally real and
`K` is a quadratic extension of `F`, and live in the `NumberField.CMExtension` namespace. Note that
results for the general case can be deduced for the CM case by using the isomorphism
`equivMaximalRealSubfield` between `F` and `K‚Å∫` mentioned above.

-/

open NumberField ComplexEmbedding InfinitePlace Algebra

open scoped ComplexConjugate

namespace NumberField

section maximalRealSubfield

/--
A number field `K` is `CM` if `K` is a totally complex quadratic extension of its maximal
real subfield `K‚Å∫`.
-/
class IsCMField (K : Type*) [Field K] [NumberField K] [IsTotallyComplex K] : Prop where
  is_quadratic : IsQuadraticExtension (maximalRealSubfield K) K

namespace IsCMField

open ComplexEmbedding

variable (K : Type*) [Field K] [NumberField K] [IsTotallyComplex K] [IsCMField K]

local notation3 "K‚Å∫" => maximalRealSubfield K

instance isQuadraticExtension : IsQuadraticExtension K‚Å∫ K :=
  IsCMField.is_quadratic

theorem card_infinitePlace_eq_card_infinitePlace :
    Fintype.card (InfinitePlace K‚Å∫) = Fintype.card (InfinitePlace K) := by
  rw [card_eq_nrRealPlaces_add_nrComplexPlaces, card_eq_nrRealPlaces_add_nrComplexPlaces,
    IsTotallyComplex.nrRealPlaces_eq_zero K, IsTotallyReal.nrComplexPlaces_eq_zero, zero_add,
    add_zero, ‚Üê IsTotallyReal.finrank, ‚Üê Nat.mul_left_cancel_iff zero_lt_two,
    ‚Üê IsTotallyComplex.finrank, ‚Üê Module.finrank_mul_finrank ‚Ñö K‚Å∫ K, mul_comm,
    IsQuadraticExtension.finrank_eq_two _ K]

theorem units_rank_eq_units_rank :
    Units.rank K‚Å∫ = Units.rank K := by
  rw [Units.rank, Units.rank, card_infinitePlace_eq_card_infinitePlace K]

theorem exists_isConj (œÜ : K ‚Üí+* ‚ÑÇ) :
    ‚àÉ œÉ : K ‚âÉ‚Çê[K‚Å∫] K, IsConj œÜ œÉ :=
  exists_isConj_of_isRamified <|
    isRamified_iff.mpr ‚ü®IsTotallyComplex.isComplex _, IsTotallyReal.isReal _‚ü©

/--
All the conjugations of a CM-field over its maximal real subfield are the same.
-/
theorem isConj_eq_isConj {œÜ œà : K ‚Üí+* ‚ÑÇ} {œÉ œÑ : K ‚âÉ‚Çê[K‚Å∫] K}
    (hœÜ : IsConj œÜ œÉ) (hœà : IsConj œà œÑ) : œÉ = œÑ := by
  have : Nat.card (K ‚âÉ‚Çê[K‚Å∫] K) = 2 :=
    (IsQuadraticExtension.finrank_eq_two K‚Å∫ K) ‚ñ∏ IsGalois.card_aut_eq_finrank K‚Å∫ K
  rw [Nat.card_eq_two_iff' 1] at this
  exact ExistsUnique.unique this
    ((isConj_ne_one_iff hœÜ).mpr <| IsTotallyComplex.complexEmbedding_not_isReal œÜ)
    ((isConj_ne_one_iff hœà).mpr <| IsTotallyComplex.complexEmbedding_not_isReal œà)

/--
The complex conjugation of the CM-field `K`.
-/
noncomputable def complexConj : K ‚âÉ‚Çê[K‚Å∫] K :=
  (exists_isConj K (Classical.choice (inferInstance : Nonempty _))).choose

/--
The complex conjugation is the conjugation of any complex embedding of a CM-field.
-/
theorem isConj_complexConj (œÜ : K ‚Üí+* ‚ÑÇ) : IsConj œÜ (complexConj K) := by
  obtain ‚ü®œÉ, hœÉ‚ü© := exists_isConj _ œÜ
  have := (exists_isConj K (Classical.choice (inferInstance : Nonempty (K ‚Üí+* ‚ÑÇ)))).choose_spec
  rwa [isConj_eq_isConj K hœÉ this] at hœÉ

@[simp]
theorem complexEmbedding_complexConj (œÜ : K ‚Üí+* ‚ÑÇ) (x : K) :
    œÜ (complexConj K x) = conj (œÜ x) := by
  rw [IsConj.eq (isConj_complexConj K œÜ), RCLike.star_def]

@[simp]
theorem complexConj_apply_apply (x : K) :
    complexConj K (complexConj K x) = x := by
  let œÜ : K ‚Üí+* ‚ÑÇ := Classical.choice (inferInstance : Nonempty _)
  exact isConj_apply_apply (isConj_complexConj K œÜ) x

theorem complexConj_ne_one :
    complexConj K ‚â† (1 : K ‚âÉ‚Çê[K‚Å∫] K) :=
  (isConj_ne_one_iff
    (exists_isConj K (Classical.choice (inferInstance : Nonempty _))).choose_spec).mpr <|
      IsTotallyComplex.complexEmbedding_not_isReal _

@[simp]
theorem complexConj_apply_eq_self (x : K‚Å∫) : complexConj K x = x := AlgEquiv.commutes _ x

/--
The complex conjugation is an automorphism of degree `2`.
-/
theorem orderOf_complexConj :
    orderOf (complexConj K) = 2 :=
  orderOf_eq_prime_iff.mpr ‚ü®by ext; simp, complexConj_ne_one K‚ü©

/--
The complex conjugation generates the Galois group of `K/K‚Å∫`.
-/
theorem zpowers_complexConj_eq_top :
    Subgroup.zpowers (complexConj K) = ‚ä§ := by
  refine Subgroup.eq_top_of_card_eq _ ?_
  rw [Nat.card_zpowers, orderOf_complexConj, IsGalois.card_aut_eq_finrank,
    IsQuadraticExtension.finrank_eq_two]

/--
An element of `K` is fixed by the complex conjugation iff it lies in `K‚Å∫`.
-/
@[simp]
theorem complexConj_eq_self_iff (x : K) :
    complexConj K x = x ‚Üî x ‚àà K‚Å∫ := by
  convert (IntermediateField.mem_fixedField_iff (‚ä§ : Subgroup (K ‚âÉ‚Çê[K‚Å∫] K)) x).symm using 1
  ¬∑ rw [‚Üê zpowers_complexConj_eq_top, Subgroup.forall_mem_zpowers]
    exact (MulAction.mem_fixedBy_zpowers_iff_mem_fixedBy (g := (complexConj K))).symm
  ¬∑ rw [IsGalois.fixedField_top, IntermediateField.mem_bot]
    aesop

protected theorem RingOfIntegers.complexConj_eq_self_iff (x : ùìû K) :
    complexConj K x = x ‚Üî ‚àÉ y : ùìû K‚Å∫, algebraMap (ùìû K‚Å∫) K y = x := by
  rw [complexConj_eq_self_iff]
  refine ‚ü®fun h ‚Ü¶ ?_, fun ‚ü®y, hy‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ have : IsIntegral ‚Ñ§ (‚ü®x, h‚ü© : K‚Å∫) :=
      (isIntegral_algebraMap_iff (FaithfulSMul.algebraMap_injective K‚Å∫ K)).mp x.isIntegral_coe
    refine ‚ü®‚ü®‚ü®x, h‚ü©, this‚ü©, ?_‚ü©
    rw [IsScalarTower.algebraMap_apply (ùìû K‚Å∫) K‚Å∫, RingOfIntegers.map_mk]
    rfl
  ¬∑ rw [‚Üê hy, IsScalarTower.algebraMap_apply (ùìû K‚Å∫) K‚Å∫]
    exact SetLike.coe_mem _

protected theorem Units.complexConj_eq_self_iff (u : (ùìû K)À£) :
    complexConj K u = u ‚Üî ‚àÉ v : (ùìû K‚Å∫)À£, algebraMap (ùìû K‚Å∫) K v = u := by
  rw [RingOfIntegers.complexConj_eq_self_iff, Units.coe_coe]
  refine ‚ü®fun ‚ü®y, hy‚ü© ‚Ü¶ ?_, fun ‚ü®v, hv‚ü© ‚Ü¶ ‚ü®v, by rw [hv]‚ü©‚ü©
  have : IsUnit y := by
    apply IsUnit.of_map (algebraMap (ùìû K‚Å∫) (ùìû K))
    rw [show algebraMap (ùìû K‚Å∫) (ùìû K) y = u by exact RingOfIntegers.ext hy]
    exact u.isUnit
  exact ‚ü®this.unit, by simp [hy]‚ü©

noncomputable instance starRing : StarRing K where
  star := complexConj K
  star_involutive _ := complexConj_apply_apply _ _
  star_mul _ _ := by rw [map_mul, mul_comm]
  star_add _ _ := by rw [map_add]

section units

open Units

/--
The subgroup of `(ùìû K)À£` generated by the units of `K‚Å∫`. These units are exactly the units fixed
by the complex conjugation, see `IsCMField.unitsComplexConj_eq_self_iff`.
-/
def realUnits : Subgroup (ùìû K)À£ := (Units.map (algebraMap (ùìû K‚Å∫) (ùìû K)).toMonoidHom).range

omit [IsTotallyComplex K] [IsCMField K] in
theorem mem_realUnits_iff (u : (ùìû K)À£) :
    u ‚àà realUnits K ‚Üî ‚àÉ v : (ùìû K‚Å∫)À£, algebraMap (ùìû K‚Å∫) (ùìû K) v = u := by
  simp [realUnits, MonoidHom.mem_range, RingHom.toMonoidHom_eq_coe, Units.ext_iff]

theorem unitsComplexConj_eq_self_iff (u : (ùìû K)À£) :
    complexConj K u = u ‚Üî u ‚àà realUnits K := by
  simp_rw [Units.complexConj_eq_self_iff, mem_realUnits_iff, RingOfIntegers.ext_iff,
    IsScalarTower.algebraMap_apply (ùìû K‚Å∫) (ùìû K) K]

/--
The image of a root of unity by the complex conjugation is its inverse.
This is the version of `Complex.conj_rootsOfUnity` for CM-fields.
-/
@[simp]
theorem unitsComplexConj_torsion (Œ∂ : torsion K) :
    complexConj K (Œ∂.val : K) = (Œ∂.val : K)‚Åª¬π := by
  let œÜ : K ‚Üí+* ‚ÑÇ := Classical.choice (inferInstance : Nonempty _)
  apply œÜ.injective
  rw [complexEmbedding_complexConj, ‚Üê Units.complexEmbedding_apply,
    Complex.conj_rootsOfUnity (n := torsionOrder K), Units.val_inv_eq_inv_val,
    Units.complexEmbedding_apply, map_inv‚ÇÄ]
  exact map_complexEmbedding_torsion K œÜ ‚ñ∏ Subgroup.apply_coe_mem_map _ _ _

end units

end IsCMField

end maximalRealSubfield

namespace CMExtension

variable (F K : Type*) [Field F] [NumberField F] [IsTotallyReal F] [Field K] [NumberField K]
  [IsTotallyComplex K] [Algebra F K] [IsQuadraticExtension F K]

theorem eq_maximalRealSubfield (E : Subfield K) [IsTotallyReal E] [IsQuadraticExtension E K] :
    E = maximalRealSubfield K := by
  refine le_antisymm (IsTotallyReal.le_maximalRealSubfield E) ?_
  by_contra! h
  have h' : E ‚äî (maximalRealSubfield K) = ‚ä§ := by
    let L : IntermediateField E K := (E ‚äî (maximalRealSubfield K)).toIntermediateField
      (fun x ‚Ü¶ (le_sup_left (a := E)) x.prop)
    have := ((IntermediateField.isSimpleOrder_of_finrank_prime E K
      (IsQuadraticExtension.finrank_eq_two E K ‚ñ∏ Nat.prime_two)).eq_bot_or_eq_top L).resolve_left ?_
    ¬∑ simpa [L] using congr_arg IntermediateField.toSubfield this
    ¬∑ contrapose! h
      rw [‚Üê SetLike.coe_set_eq, Subfield.coe_toIntermediateField] at h
      rw [‚Üê sup_eq_left, ‚Üê SetLike.coe_set_eq, h, IntermediateField.coe_bot]
      aesop
  have : IsTotallyReal K := (h' ‚ñ∏ isTotallyReal_sup).ofRingEquiv Subring.topEquiv
  obtain w : InfinitePlace K := Classical.choice (inferInstance : Nonempty _)
  exact (not_isReal_iff_isComplex.mpr (IsTotallyComplex.isComplex w)) (IsTotallyReal.isReal w)

/--
Any field `F` such that `K/F` is a CM-extension is isomorphic to the maximal real subfield of `K`.
-/
noncomputable def equivMaximalRealSubfield :
    F ‚âÉ+* maximalRealSubfield K :=
  (algebraMap F K).rangeRestrictFieldEquiv.trans (RingEquiv.subfieldCongr (by
    have := IsTotallyReal.ofRingEquiv (algebraMap F K).rangeRestrictFieldEquiv
    have : IsQuadraticExtension (algebraMap F K).fieldRange K :=
    { finrank_eq_two' :=
        (IsQuadraticExtension.finrank_eq_two F K) ‚ñ∏ Algebra.finrank_eq_of_equiv_equiv
          (algebraMap F K).rangeRestrictFieldEquiv.symm (RingEquiv.refl K) (by ext; simp; rfl) }
    exact eq_maximalRealSubfield K (algebraMap F K).fieldRange))

@[simp]
theorem equivMaximalRealSubfield_apply (x : F) :
    equivMaximalRealSubfield F K x = algebraMap F K x := rfl

@[simp]
theorem algebraMap_equivMaximalRealSubfield_symm_apply (x : maximalRealSubfield K) :
    algebraMap F K ((CMExtension.equivMaximalRealSubfield F K).symm x) =
      algebraMap (maximalRealSubfield K) K x := by
  simpa using (equivMaximalRealSubfield_apply F K ((equivMaximalRealSubfield F K).symm x)).symm

include F in
/--
If `K/F` is a CM-extension then `K` is a CM-field.
-/
theorem _root_.NumberField.IsCMField.ofCMExtension :
    IsCMField K where
  is_quadratic := ‚ü®(IsQuadraticExtension.finrank_eq_two F K) ‚ñ∏ finrank_eq_of_equiv_equiv
      (CMExtension.equivMaximalRealSubfield F K).symm (RingEquiv.refl K) (by ext; simp)‚ü©

open IntermediateField in
/--
A totally complex field that has a unique complex conjugation is CM.
-/
theorem _root_.NumberField.IsCMField.of_forall_isConj {œÉ : K ‚âÉ‚Çê[‚Ñö] K}
    (hœÉ : ‚àÄ œÜ : K ‚Üí+* ‚ÑÇ, IsConj œÜ œÉ) : IsCMField K := by
  have : IsTotallyReal (fixedField (Subgroup.zpowers œÉ)) := ‚ü®fun w ‚Ü¶ by
    obtain ‚ü®W, rfl‚ü© := w.comap_surjective (K := K)
    let œÑ := subgroupEquivAlgEquiv _ ‚ü®œÉ, Subgroup.mem_zpowers œÉ‚ü©
    have hœÑ : IsConj W.embedding œÑ := hœÉ _
    simpa [‚Üê isReal_mk_iff, ‚Üê InfinitePlace.comap_mk, mk_embedding] using hœÑ.isReal_comp‚ü©
  have : IsQuadraticExtension (fixedField (Subgroup.zpowers œÉ)) K := ‚ü®by
    let œÜ : K ‚Üí+* ‚ÑÇ := Classical.choice (inferInstance : Nonempty _)
    have hœÉ' : œÉ ‚â† 1 :=
      (isConj_ne_one_iff (hœÉ œÜ)).mpr <| IsTotallyComplex.complexEmbedding_not_isReal œÜ
    rw [finrank_fixedField_eq_card, Nat.card_zpowers, orderOf_isConj_two_of_ne_one (hœÉ œÜ) hœÉ']‚ü©
  exact IsCMField.ofCMExtension (fixedField (Subgroup.zpowers œÉ)) K

/--
A totally complex abelian extension of `‚Ñö` is CM.
-/
instance of_isMulCommutative [IsGalois ‚Ñö K] [IsMulCommutative (K ‚âÉ‚Çê[‚Ñö] K)] :
    IsCMField K := by
  let œÜ : K ‚Üí+* ‚ÑÇ := Classical.choice (inferInstance : Nonempty _)
  obtain ‚ü®œÉ, hœÉ‚ÇÅ‚ü© : ‚àÉ œÉ : K ‚âÉ‚Çê[‚Ñö] K, ComplexEmbedding.IsConj œÜ œÉ :=
    exists_isConj_of_isRamified <|
      isRamified_iff.mpr ‚ü®IsTotallyComplex.isComplex _, IsTotallyReal.isReal _‚ü©
  have hœÉ‚ÇÇ : ‚àÄ (œÜ : K ‚Üí+* ‚ÑÇ), ComplexEmbedding.IsConj œÜ œÉ := by
    intro œà
    obtain ‚ü®ŒΩ, rfl‚ü© := exists_comp_symm_eq_of_comp_eq (k := ‚Ñö) œÜ œà (by ext; simp)
    rw [show œÉ = ŒΩ.symm‚Åª¬π * œÉ * ŒΩ.symm by simp]
    exact hœÉ‚ÇÅ.comp _
  exact IsCMField.of_forall_isConj K hœÉ‚ÇÇ

end CMExtension

end NumberField
