/-
Copyright (c) 2025 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.NumberTheory.LSeries.SumCoeff
import Mathlib.NumberTheory.NumberField.Ideal
import Mathlib.Algebra.BigOperators.Ring.Nat

/-!
# The Dedekind zeta function of a number field

In this file, we define and prove results about the Dedekind zeta function of a number field.

## Main definitions and results

* `NumberField.dedekindZeta`: the Dedekind zeta function.
* `NumberField.dedekindZeta_residue`: **Dirichlet class number formula** computation of the
  residue of the dedekind zeta function at `s = 1`.

# TODO

Generalize the construction of the Dedekind zeta function.
-/

variable (K : Type*) [Field K] [NumberField K]

noncomputable section

open Filter Ideal NumberField.InfinitePlace NumberField.Units Topology nonZeroDivisors

namespace NumberField

open scoped Real

/--
The Dedekind zeta function of a number field. It is defined as the `L`-series with coefficients
the number of integral ideals of norm `n`.
-/
def dedekindZeta (s : ‚ÑÇ) :=
  LSeries (fun n ‚Ü¶ Nat.card {I : Ideal (ùìû K) // absNorm I = n}) s

/--
The value of the residue at `s = 1` of the Dedekind zeta function, see `dedekindZeta_residue`.
-/
def residue : ‚Ñù :=
  (2 ^ nrRealPlaces K * (2 * œÄ) ^ nrComplexPlaces K * regulator K * classNumber K) /
    (torsionOrder K *  Real.sqrt |discr K|)

theorem residue_pos : 0 < residue K := by
  refine div_pos ?_ ?_
  ¬∑ exact mul_pos (mul_pos (by positivity) (regulator_pos K)) (Nat.cast_pos.mpr (classNumber_pos K))
  ¬∑ exact mul_pos (Nat.cast_pos.mpr (torsionOrder_pos K)) <|
      Real.sqrt_pos_of_pos <| abs_pos.mpr (Int.cast_ne_zero.mpr (discr_ne_zero K))

theorem residue_ne_zero : residue K ‚â† 0 := (residue_pos K).ne'

/-
**Dirichlet class number formula**
-/
theorem dedekindZeta_residue :
    Tendsto (fun s  : ‚Ñù ‚Ü¶ (s - 1) * dedekindZeta K s) (ùìù[>] 1) (ùìù (residue K)) := by
  refine LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div_and_nonneg _ ?_
    (fun _ ‚Ü¶ Nat.cast_nonneg _)
  refine ((Ideal.tendsto_norm_le_div_atTop‚ÇÄ K).comp tendsto_natCast_atTop_atTop).congr fun n ‚Ü¶ ?_
  simp only [Function.comp_apply, Nat.cast_le, ‚Üê Nat.cast_sum]
  congr
  rw [‚Üê add_left_inj 1, ‚Üê card_norm_le_eq_card_norm_le_add_one]
  rw [show Finset.Icc 1 n = Finset.Ioc 0 n by rfl]
  rw [show 1 = Nat.card {I : Ideal (ùìû K) // absNorm I = 0} by simp [Ideal.absNorm_eq_zero_iff]]
  rw [Finset.sum_Ioc_add_eq_sum_Icc (n.zero_le),
    ‚Üê Finset.card_preimage_eq_sum_card_image_eq (fun k _ ‚Ü¶ finite_setOf_absNorm_eq k)]
  simp [Set.coe_eq_subtype]

end NumberField
