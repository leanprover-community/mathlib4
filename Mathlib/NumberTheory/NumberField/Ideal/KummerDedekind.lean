/-
Copyright (c) 2025 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.NumberTheory.KummerDedekind
import Mathlib.NumberTheory.NumberField.Basic
import Mathlib.RingTheory.Ideal.Norm.AbsNorm
import Mathlib.RingTheory.Ideal.Int

/-!
# Kummer-Dedekind criterion for the splitting of prime numbers

In this file, we give a specialized version of the Kummer-Dedekind criterion for the case of the
splitting of rational primes in number fields.

## Main definitions

* `RingOfIntegers.exponent`: the smallest positive integer `d` contained in the conductor of `Î¸`.
  It is the smallest integer such that `d â€¢ ğ“ K âŠ† â„¤[Î¸]`, see `RingOfIntegers.exponent_eq_sInf`.

* `RingOfIntegers.ZModXQuotSpanEquivQuotSpan`: The isomorphism between `(â„¤ / pâ„¤)[X] / (minpoly Î¸)`
  and `ğ“ K / p(ğ“ K)` for a prime `p` which doesn't divide the exponent of `Î¸`.

-/

noncomputable section

open Polynomial NumberField Ideal KummerDedekind UniqueFactorizationMonoid

variable {K : Type*} [Field K]

namespace RingOfIntegers

/--
The smallest positive integer `d` contained in the conductor of `Î¸`. It is the smallest integer
such that `d â€¢ ğ“ K âŠ† â„¤[Î¸]`, see `exponent_eq_sInf`. It is set to `0` if `d` does not exists.
-/
def exponent (Î¸ : ğ“ K) : â„• := absNorm (under â„¤ (conductor â„¤ Î¸))

variable {Î¸ : ğ“ K}

theorem exponent_eq_one_iff : exponent Î¸ = 1 â†” Algebra.adjoin â„¤ {Î¸} = âŠ¤ := by
  rw [exponent, absNorm_eq_one_iff, comap_eq_top_iff, conductor_eq_top_iff_adjoin_eq_top]

theorem not_dvd_exponent_iff {p : â„•} [Fact (Nat.Prime p)] :
    Â¬ p âˆ£ exponent Î¸ â†” Codisjoint (comap (algebraMap â„¤ (ğ“ K)) (conductor â„¤ Î¸)) (span {â†‘p}) := by
  rw [codisjoint_comm, â† IsCoatom.not_le_iff_codisjoint, â† under_def, â† Ideal.dvd_iff_le,
    â† Int.ideal_span_absNorm_eq_self (under â„¤ (conductor â„¤ Î¸)),
    span_singleton_dvd_span_singleton_iff_dvd, Int.natCast_dvd_natCast, exponent]
  exact isMaximal_def.mp <| Int.ideal_span_isMaximal_of_prime p

theorem exponent_eq_sInf : exponent Î¸ = sInf {d : â„• | 0 < d âˆ§ (d : ğ“ K) âˆˆ conductor â„¤ Î¸} := by
  rw [exponent, Int.absNorm_under_eq_sInf]

variable [NumberField K] {Î¸ : ğ“ K} {p : â„•} [Fact p.Prime]

/--
If `p` doesn't divide the exponent of `Î¸`, then `(â„¤ / pâ„¤)[X] / (minpoly Î¸) â‰ƒ+* ğ“ K / p(ğ“ K)`.
-/
def ZModXQuotSpanEquivQuotSpan (hp : Â¬ p âˆ£ exponent Î¸) :
    (ZMod p)[X] â§¸ span {map (Int.castRingHom (ZMod p)) (minpoly â„¤ Î¸)} â‰ƒ+*
      ğ“ K â§¸ span {(p : ğ“ K)} :=
  (quotientEquivAlgOfEq â„¤ (by simp [Ideal.map_span, Polynomial.map_map])).toRingEquiv.trans
    ((quotientEquiv _ _ (mapEquiv (Int.quotientSpanNatEquivZMod p)) rfl).symm.trans
      ((quotMapEquivQuotQuotMap (not_dvd_exponent_iff.mp hp).eq_top Î¸.isIntegral).symm.trans
        (quotientEquivAlgOfEq â„¤ (by simp [map_span])).toRingEquiv))

theorem ZModXQuotSpanEquivQuotSpan_mk_apply (hp : Â¬ p âˆ£ exponent Î¸) (Q : â„¤[X]) :
    (ZModXQuotSpanEquivQuotSpan hp)
      (Ideal.Quotient.mk (span {map (Int.castRingHom (ZMod p)) (minpoly â„¤ Î¸)})
      (map (Int.castRingHom (ZMod p)) Q)) = Ideal.Quotient.mk (span {(p : ğ“ K)}) (aeval Î¸ Q) := by
  simp only [ZModXQuotSpanEquivQuotSpan, AlgEquiv.toRingEquiv_eq_coe, algebraMap_int_eq,
    RingEquiv.trans_apply, AlgEquiv.coe_ringEquiv, quotientEquivAlgOfEq_mk,
    quotientEquiv_symm_apply, quotientMap_mk, RingHom.coe_coe, mapEquiv_symm_apply,
    Polynomial.map_map, Int.quotientSpanNatEquivZMod_comp_castRingHom]
  exact congr_arg (quotientEquivAlgOfEq â„¤ (by simp [map_span])) <|
    quotMapEquivQuotQuotMap_symm_apply (not_dvd_exponent_iff.mp hp).eq_top Î¸.isIntegral Q

variable (p Î¸) in
/--
The finite set of monic irreducible factors of `minpoly â„¤ Î¸` modulo `p`.
-/
abbrev monicFactorsMod : Finset ((ZMod p)[X]) :=
  (normalizedFactors (map (Int.castRingHom (ZMod p)) (minpoly â„¤ Î¸))).toFinset

/--
If `p` does not divide `exponent Î¸` and `Q` is a lift of a monic irreducible factor of
`minpoly â„¤ Î¸` modulo `p`, then `(â„¤ / pâ„¤)[X] / Q â‰ƒ+* ğ“ K / (p, Q(Î¸))`.
-/
def ZModXQuotSpanEquivQuotSpanPair (hp : Â¬ p âˆ£ exponent Î¸) {Q : â„¤[X]}
    (hQ : Q.map (Int.castRingHom (ZMod p)) âˆˆ monicFactorsMod Î¸ p) :
    (ZMod p)[X] â§¸ span {Polynomial.map (Int.castRingHom (ZMod p)) Q} â‰ƒ+*
      ğ“ K â§¸ span {(p : ğ“ K), (aeval Î¸) Q} :=
  have hâ‚€ : map (Int.castRingHom (ZMod p)) (minpoly â„¤ Î¸) â‰  0 :=
      map_monic_ne_zero (minpoly.monic Î¸.isIntegral)
  have h_eqâ‚ : span {map (Int.castRingHom (ZMod p)) Q} =
      span {map (Int.castRingHom (ZMod p)) (minpoly â„¤ Î¸)} âŠ”
        span {map (Int.castRingHom (ZMod p)) Q} := by
    rw [â† span_insert, span_pair_comm, span_pair_eq_span_left_iff_dvd.mpr]
    simp only [Multiset.mem_toFinset] at hQ
    exact ((Polynomial.mem_normalizedFactors_iff hâ‚€).mp hQ).2.2
  have h_eqâ‚‚ : span {â†‘p} âŠ” span {(aeval Î¸) Q} = span {â†‘p, (aeval Î¸) Q} := by
    rw [span_insert]
  ((Ideal.quotEquivOfEq h_eqâ‚).trans (DoubleQuot.quotQuotEquivQuotSup _ _).symm).trans <|
    (Ideal.quotientEquiv
      (Ideal.map (Ideal.Quotient.mk _) (span {(Polynomial.map (Int.castRingHom (ZMod p)) Q)}))
      (Ideal.map (Ideal.Quotient.mk _) (span {aeval Î¸ Q})) (ZModXQuotSpanEquivQuotSpan hp) (by
        simp [map_span, ZModXQuotSpanEquivQuotSpan_mk_apply])).trans <|
    (DoubleQuot.quotQuotEquivQuotSup _ _).trans (Ideal.quotEquivOfEq h_eqâ‚‚)

end RingOfIntegers
