/-
Copyright (c) 2025 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.NumberTheory.KummerDedekind
import Mathlib.NumberTheory.NumberField.Basic
import Mathlib.NumberTheory.RamificationInertia.Basic
import Mathlib.RingTheory.Ideal.Int

/-!
# Kummer-Dedekind criterion for the splitting of prime numbers

In this file, we give a specialized version of the Kummer-Dedekind criterion for the case of the
splitting of rational primes in number fields.

## Main definitions

* TODO

## Main results

* TODO

-/

noncomputable section

open Polynomial NumberField Ideal KummerDedekind UniqueFactorizationMonoid

variable {K : Type*} [Field K]

namespace RingOfIntegers

/--
The smallest positive integer `d` such that `d â€¢ ğ“ K âŠ† â„¤[Î¸]`, see `exponent_eq_sInf`. It is equal
to `0` if `d` does not exists.
-/
def exponent (Î¸ : ğ“ K) : â„• := absNorm (under â„¤ (conductor â„¤ Î¸))

variable {Î¸ : ğ“ K}

theorem exponent_eq_one_iff :
    exponent Î¸ = 1 â†” Algebra.adjoin â„¤ {Î¸} = âŠ¤ := by
  rw [exponent, absNorm_eq_one_iff, comap_eq_top_iff, conductor_eq_top_iff_adjoin_eq_top]

theorem not_dvd_exponent_iff {p : â„•} [Fact (Nat.Prime p)] :
    Â¬ p âˆ£ exponent Î¸ â†” comap (algebraMap â„¤ (ğ“ K)) (conductor â„¤ Î¸) âŠ” span {(p : â„¤)} = âŠ¤ := by
  rw [sup_comm, IsCoatom.sup_eq_top_iff, â† under_def, â† Ideal.dvd_iff_le,
    Int.ideal_eq_span_absNorm_self (under â„¤ (conductor â„¤ Î¸)),
    span_singleton_dvd_span_singleton_iff_dvd, Int.natCast_dvd_natCast, exponent]
  exact isMaximal_def.mp <| Int.ideal_span_isMaximal_of_prime p

theorem exponent_eq_sInf :
    exponent Î¸ = sInf {d : â„• | 0 < d âˆ§ (d : ğ“ K) âˆˆ conductor â„¤ Î¸} := by
  rw [exponent, Int.absNorm_under_eq_sInf]

variable [NumberField K] {Î¸ : ğ“ K} {p : â„•} [Fact (Nat.Prime p)]

/--
If `p` doesn't divide the exponent of `Î¸`, then `(â„¤ / pâ„¤)[X] / (minpoly Î¸) â‰ƒ+* ğ“ K / p(ğ“ K)`.
-/
def ZModXQuotSpanEquivQuotSpan (hp : Â¬ p âˆ£ exponent Î¸) :
    (ZMod p)[X] â§¸ span {map (Int.castRingHom (ZMod p)) (minpoly â„¤ Î¸)} â‰ƒ+*
      ğ“ K â§¸ span {(p : ğ“ K)} :=
  (quotientEquivAlgOfEq â„¤ (by simp [Ideal.map_span, Polynomial.map_map])).toRingEquiv.trans
    ((quotientEquiv _ _ (mapEquiv (Int.quotientSpanNatEquivZMod p)) rfl).symm.trans
      ((quotMapEquivQuotQuotMap (not_dvd_exponent_iff.mp hp) Î¸.isIntegral).symm.trans
        (quotientEquivAlgOfEq â„¤ (by simp [map_span])).toRingEquiv))

theorem ZModXQuotSpanEquivQuotSpan_mk_apply (hp : Â¬ p âˆ£ exponent Î¸) (Q : â„¤[X]) :
  (ZModXQuotSpanEquivQuotSpan hp)
    (Ideal.Quotient.mk (span {map (Int.castRingHom (ZMod p)) (minpoly â„¤ Î¸)})
      (map (Int.castRingHom (ZMod p)) Q)) = Ideal.Quotient.mk (span {(p : ğ“ K)}) (aeval Î¸ Q) := by
  unfold ZModXQuotSpanEquivQuotSpan
  simp only [AlgEquiv.toRingEquiv_eq_coe, algebraMap_int_eq, RingEquiv.trans_apply,
    AlgEquiv.coe_ringEquiv, quotientEquivAlgOfEq_mk, quotientEquiv_symm_apply, quotientMap_mk,
    RingHom.coe_coe, mapEquiv_symm_apply, Polynomial.map_map,
    Int.quotientSpanNatEquivZMod_comp_castRingHom]
  exact congr_arg (quotientEquivAlgOfEq â„¤ (by simp [map_span])) <|
    quotMapEquivQuotQuotMap_symm_apply (not_dvd_exponent_iff.mp hp) Î¸.isIntegral Q

variable (p Î¸) in
/--
The set of monic irreducible factors of `minpoly â„¤ Î¸` modulo `p`.
-/
abbrev monicFactorsMod : Set ((ZMod p)[X]) :=
  (normalizedFactors (map (Int.castRingHom (ZMod p)) (minpoly â„¤ Î¸))).toFinset

/--
If `p` does not divide `exponent Î¸` and `Q` is a lift of a monic irreducible factor of
`minpoly â„¤ Î¸` modulo `p`, then `(â„¤ / pâ„¤)[X] / Q â‰ƒ+* ğ“ K / (p, Q(Î¸))`.
-/
def ZModXQuotSpanEquivQuotSpanPair (hp : Â¬ p âˆ£ exponent Î¸) {Q : â„¤[X]}
    (hQ : Q.map (Int.castRingHom (ZMod p)) âˆˆ monicFactorsMod Î¸ p) :
    (ZMod p)[X] â§¸ span {Polynomial.map (Int.castRingHom (ZMod p)) Q} â‰ƒ+*
      ğ“ K â§¸ span {(p : ğ“ K), (aeval Î¸) Q} :=
  have hâ‚€ : map (Int.castRingHom (ZMod p)) (minpoly â„¤ Î¸) â‰  0 :=
      map_monic_ne_zero (minpoly.monic Î¸.isIntegral)
  have h_eqâ‚ : span {map (Int.castRingHom (ZMod p)) Q} =
      span {map (Int.castRingHom (ZMod p)) (minpoly â„¤ Î¸)} âŠ”
        span {map (Int.castRingHom (ZMod p)) Q} := by
    rw [â† span_insert, span_pair_comm, span_pair_eq_span_singleton_iff_dvd.mpr]
    simp only [Finset.mem_coe, Multiset.mem_toFinset] at hQ
    exact ((Polynomial.mem_normalizedFactors_iff hâ‚€).mp hQ).2.2
  have h_eqâ‚‚ : span {â†‘p} âŠ” span {(aeval Î¸) Q} = span {â†‘p, (aeval Î¸) Q} := by
    rw [span_insert]
  ((Ideal.quotEquivOfEq h_eqâ‚).trans (DoubleQuot.quotQuotEquivQuotSup _ _).symm).trans <|
    (Ideal.quotientEquiv
      (Ideal.map (Ideal.Quotient.mk _) (span {(Polynomial.map (Int.castRingHom (ZMod p)) Q)}))
      (Ideal.map (Ideal.Quotient.mk _) (span {aeval Î¸ Q})) (ZModXQuotSpanEquivQuotSpan hp) (by
        simp [Ideal.map_map, map_span, ZModXQuotSpanEquivQuotSpan_mk_apply])).trans <|
    (DoubleQuot.quotQuotEquivQuotSup _ _).trans (Ideal.quotEquivOfEq h_eqâ‚‚)

end RingOfIntegers

open RingOfIntegers IsDedekindDomain
namespace NumberField

variable {Î¸ : ğ“ K} {p : â„•} [Fact (Nat.Prime p)]

attribute [local instance] Int.ideal_span_isMaximal_of_prime Ideal.Quotient.field

open scoped Classical in
private def Ideal.primesOverSpanEquivMonicFactorsModAux (A : â„¤[X]) :
    {Q | Q âˆˆ normalizedFactors (map (Ideal.Quotient.mk (span {(p : â„¤)})) A)} â‰ƒ
    (normalizedFactors (map (Int.castRingHom (ZMod p)) A)).toFinset :=
  (normalizedFactorsEquiv (f := (mapEquiv (Int.quotientSpanNatEquivZMod p)).toMulEquiv)
    (by simp) (map (Ideal.Quotient.mk (span {(p : â„¤)})) A)).trans
      (Equiv.setCongr (by
        classical
        ext
        simp_rw [RingEquiv.toMulEquiv_eq_coe, RingEquiv.coe_toMulEquiv, mapEquiv_apply,
          Set.mem_setOf_eq, Multiset.toFinset_val, Set.mem_def, Multiset.mem_dedup,
          Polynomial.map_map, Int.quotientSpanNatEquivZMod_comp_Quotient_mk]))

private theorem Ideal.primesOverSpanEquivMonicFactorsModAux_symm_apply (A : â„¤[X]) {Q : (ZMod p)[X]}
    (hQ : Q âˆˆ (normalizedFactors (map (Int.castRingHom (ZMod p)) A)).toFinset) :
    ((Ideal.primesOverSpanEquivMonicFactorsModAux A).symm âŸ¨Q, hQâŸ© : (â„¤ â§¸ span {(p : â„¤)})[X]) =
      Polynomial.map ((Int.quotientSpanNatEquivZMod p).symm) Q := rfl

variable [NumberField K]

open scoped Classical in
/--
If `p` does not divide `exponent Î¸`, then the prime ideals above `p` in `K` are in bijection
with the monic irreducible factors of `minpoly â„¤ Î¸` modulo `p`.
-/
def Ideal.primesOverSpanEquivMonicFactorsMod (hp : Â¬ p âˆ£ exponent Î¸) :
    primesOver (span {(p : â„¤)}) (ğ“ K) â‰ƒ monicFactorsMod Î¸ p :=
  have h : span {(p : â„¤)} â‰  âŠ¥ := by simp [NeZero.ne p]
  ((Equiv.setCongr (by ext; simp [mem_primesOver_iff_mem_normalizedFactors _ h])).trans
    (normalizedFactorsMapEquivNormalizedFactorsMinPolyMk
    (Int.ideal_span_isMaximal_of_prime p) h (not_dvd_exponent_iff.mp hp) Î¸.isIntegral)).trans <|
      (Ideal.primesOverSpanEquivMonicFactorsModAux _)

theorem Ideal.primesOverSpanEquivMonicFactorsMod_symm_apply (hp : Â¬ p âˆ£ exponent Î¸)
    {Q : (ZMod p)[X]} (hQ : Q âˆˆ monicFactorsMod Î¸ p) :
    ((Ideal.primesOverSpanEquivMonicFactorsMod hp).symm âŸ¨Q, hQâŸ© : Ideal (ğ“ K)) =
      (normalizedFactorsMapEquivNormalizedFactorsMinPolyMk
        inferInstance (by simp [NeZero.ne p]) (not_dvd_exponent_iff.mp hp) Î¸.isIntegral).symm
        âŸ¨Q.map (Int.quotientSpanNatEquivZMod p).symm, by
          rw [â† Ideal.primesOverSpanEquivMonicFactorsModAux_symm_apply]
          exact ((Ideal.primesOverSpanEquivMonicFactorsModAux _).symm âŸ¨Q, hQâŸ©).coe_propâŸ© := rfl

theorem Ideal.primesOverSpanEquivMonicFactorsMod_symm_apply_eq_span
    (hp : Â¬ p âˆ£ exponent Î¸) {Q : â„¤[X]}
    (hQ : Q.map (Int.castRingHom (ZMod p)) âˆˆ monicFactorsMod Î¸ p) :
    ((primesOverSpanEquivMonicFactorsMod hp).symm
      âŸ¨Q.map (Int.castRingHom (ZMod p)), hQâŸ© : Ideal (ğ“ K)) =
        span {(p : (ğ“ K)), aeval Î¸ Q} := by
  simp only [primesOverSpanEquivMonicFactorsMod_symm_apply, Polynomial.map_map,
    Int.quotientSpanNatEquivZMod_comp_castRingHom]
  rw [normalizedFactorsMapEquivNormalizedFactorsMinPolyMk_symm_apply_eq_span,
    span_union, span_eq, map_span, Set.image_singleton, map_natCast, â† span_insert]

theorem Ideal.liesOver_primesOverSpanEquivMonicFactorsMod_symm (hp : Â¬ â†‘p âˆ£ exponent Î¸) {Q : â„¤[X]}
    (hQ : Q.map (Int.castRingHom (ZMod p)) âˆˆ monicFactorsMod Î¸ p) :
    LiesOver (span {(p : (ğ“ K)), aeval Î¸ Q}) (span {(p : â„¤)}) := by
  rw [â† Ideal.primesOverSpanEquivMonicFactorsMod_symm_apply_eq_span hp hQ]
  exact ((primesOverSpanEquivMonicFactorsMod hp).symm âŸ¨_, hQâŸ©).prop.2

theorem Ideal.inertiaDeg_primesOverSpanEquivMonicFactorsMod_symm_apply (hp : Â¬ p âˆ£ exponent Î¸)
    {Q : â„¤[X]} (hQ : Q.map (Int.castRingHom (ZMod p)) âˆˆ monicFactorsMod Î¸ p) :
    inertiaDeg (span {(p : â„¤)}) ((primesOverSpanEquivMonicFactorsMod hp).symm
      âŸ¨Q.map (Int.castRingHom (ZMod p)), hQâŸ© : Ideal (ğ“ K)) =
        natDegree (Q.map (Int.castRingHom (ZMod p))) := by
  -- Register this instance for `inertiaDeg_algebraMap` below
  have := liesOver_primesOverSpanEquivMonicFactorsMod_symm hp hQ
  have hQ' : Polynomial.map (Int.castRingHom (ZMod p)) Q â‰  0 := by
    contrapose! hQ
    rw [hQ]
    simp only [Finset.mem_coe, Multiset.mem_toFinset]
    exact zero_not_mem_normalizedFactors _
  rw [primesOverSpanEquivMonicFactorsMod_symm_apply_eq_span, inertiaDeg_algebraMap,
    â† finrank_quotient_span_eq_natDegree hQ']
  refine Algebra.finrank_eq_of_equiv_equiv (Int.quotientSpanNatEquivZMod p) ?_ ?_
  Â· exact (ZModXQuotSpanEquivQuotSpanPair hp hQ).symm
  Â· ext; simp

theorem Ideal.inertiaDeg_primesOverSpanEquivMonicFactorsMod_symm_apply' (hp : Â¬ p âˆ£ exponent Î¸)
    {Q : (ZMod p)[X]} (hQ : Q âˆˆ monicFactorsMod Î¸ p) :
    inertiaDeg (span {(p : â„¤)})
      ((primesOverSpanEquivMonicFactorsMod hp).symm âŸ¨Q, hQâŸ© : Ideal (ğ“ K)) =
        natDegree Q := by
  obtain âŸ¨S, rflâŸ© := (map_surjective _ (ZMod.ringHom_surjective (Int.castRingHom (ZMod p)))) Q
  rw [inertiaDeg_primesOverSpanEquivMonicFactorsMod_symm_apply]

theorem Ideal.ramificationIdx_primesOverSpanEquivMonicFactorsMod_symm_apply (hp : Â¬ p âˆ£ exponent Î¸)
    {Q : â„¤[X]} (hQ : Q.map (Int.castRingHom (ZMod p)) âˆˆ monicFactorsMod Î¸ p) :
    ramificationIdx (algebraMap â„¤ (ğ“ K)) (span {(p : â„¤)})
      ((primesOverSpanEquivMonicFactorsMod hp).symm
        âŸ¨Q.map (Int.castRingHom (ZMod p)), hQâŸ© : Ideal (ğ“ K)) =
          multiplicity (Q.map (Int.castRingHom (ZMod p)))
            ((minpoly â„¤ Î¸).map (Int.castRingHom (ZMod p))) := by
  have h : span {(p : â„¤)} â‰  âŠ¥ := by simp [NeZero.ne p]
  rw [ramificationIdx_eq_multiplicity (RingHom.injective_int _) (by simp [NeZero.ne p])
    inferInstance]
  Â· apply multiplicity_eq_of_emultiplicity_eq
    rw [â† emultiplicity_map_eq (mapEquiv (Int.quotientSpanNatEquivZMod p).symm),
      emultiplicity_factors_map_eq_emultiplicity inferInstance (by simp [NeZero.ne p])
      (not_dvd_exponent_iff.mp hp) Î¸.isIntegral]
    Â· simp_rw [primesOverSpanEquivMonicFactorsMod_symm_apply]
      rw [Equiv.apply_symm_apply]
      simp [Polynomial.map_map]
    Â· rw [â† mem_primesOver_iff_mem_normalizedFactors _ h]
      exact ((primesOverSpanEquivMonicFactorsMod hp).symm âŸ¨_, hQâŸ©).coe_prop
  Â· have := ((primesOverSpanEquivMonicFactorsMod hp).symm âŸ¨_, hQâŸ©).prop.2
    exact Ideal.ne_bot_of_liesOver_of_ne_bot h _

theorem Ideal.ramificationIdx_primesOverSpanEquivMonicFactorsMod_symm_apply' (hp : Â¬ p âˆ£ exponent Î¸)
    {Q : (ZMod p)[X]} (hQ : Q âˆˆ monicFactorsMod Î¸ p) :
    ramificationIdx (algebraMap â„¤ (ğ“ K)) (span {(p : â„¤)})
      ((primesOverSpanEquivMonicFactorsMod hp).symm âŸ¨Q, hQâŸ© : Ideal (ğ“ K)) =
        multiplicity Q ((minpoly â„¤ Î¸).map (Int.castRingHom (ZMod p))) := by
  obtain âŸ¨S, rflâŸ© := (map_surjective _ (ZMod.ringHom_surjective (Int.castRingHom (ZMod p)))) Q
  rw [ramificationIdx_primesOverSpanEquivMonicFactorsMod_symm_apply]

end NumberField
