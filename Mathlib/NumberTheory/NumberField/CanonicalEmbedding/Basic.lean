/-
Copyright (c) 2022 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.Algebra.Module.Zlattice.Basic
import Mathlib.NumberTheory.NumberField.Embeddings
import Mathlib.NumberTheory.NumberField.FractionalIdeal

#align_import number_theory.number_field.canonical_embedding from "leanprover-community/mathlib"@"60da01b41bbe4206f05d34fd70c8dd7498717a30"

/-!
# Canonical embedding of a number field

The canonical embedding of a number field `K` of degree `n` is the ring homomorphism
`K ‚Üí+* ‚ÑÇ^n` that sends `x ‚àà K` to `(œÜ_‚ÇÅ(x),...,œÜ_n(x))` where the `œÜ_i`'s are the complex
embeddings of `K`. Note that we do not choose an ordering of the embeddings, but instead map `K`
into the type `(K ‚Üí+* ‚ÑÇ) ‚Üí ‚ÑÇ` of `‚ÑÇ`-vectors indexed by the complex embeddings.

## Main definitions and results

* `NumberField.canonicalEmbedding`: the ring homomorphism `K ‚Üí+* ((K ‚Üí+* ‚ÑÇ) ‚Üí ‚ÑÇ)` defined by
sending `x : K` to the vector `(œÜ x)` indexed by `œÜ : K ‚Üí+* ‚ÑÇ`.

* `NumberField.canonicalEmbedding.integerLattice.inter_ball_finite`: the intersection of the
image of the ring of integers by the canonical embedding and any ball centered at `0` of finite
radius is finite.

* `NumberField.mixedEmbedding`: the ring homomorphism from `K ‚Üí+* ({ w // IsReal w } ‚Üí ‚Ñù) √ó
({ w // IsComplex w } ‚Üí ‚ÑÇ)` that sends `x ‚àà K` to `(œÜ_w x)_w` where `œÜ_w` is the embedding
associated to the infinite place `w`. In particular, if `w` is real then `œÜ_w : K ‚Üí+* ‚Ñù` and, if
`w` is complex, `œÜ_w` is an arbitrary choice between the two complex embeddings defining the place
`w`.

## Tags

number field, infinite places
-/

variable (K : Type*) [Field K]

namespace NumberField.canonicalEmbedding

open NumberField

/-- The canonical embedding of a number field `K` of degree `n` into `‚ÑÇ^n`. -/
def _root_.NumberField.canonicalEmbedding : K ‚Üí+* ((K ‚Üí+* ‚ÑÇ) ‚Üí ‚ÑÇ) := Pi.ringHom fun œÜ => œÜ

theorem _root_.NumberField.canonicalEmbedding_injective [NumberField K] :
    Function.Injective (NumberField.canonicalEmbedding K) := RingHom.injective _

variable {K}

@[simp]
theorem apply_at (œÜ : K ‚Üí+* ‚ÑÇ) (x : K) : (NumberField.canonicalEmbedding K x) œÜ = œÜ x := rfl

open scoped ComplexConjugate

/-- The image of `canonicalEmbedding` lives in the `‚Ñù`-submodule of the `x ‚àà ((K ‚Üí+* ‚ÑÇ) ‚Üí ‚ÑÇ)` such
that `conj x_œÜ = x_(conj œÜ)` for all `‚àÄ œÜ : K ‚Üí+* ‚ÑÇ`. -/
theorem conj_apply {x : ((K ‚Üí+* ‚ÑÇ) ‚Üí ‚ÑÇ)} (œÜ : K ‚Üí+* ‚ÑÇ)
    (hx : x ‚àà Submodule.span ‚Ñù (Set.range (canonicalEmbedding K))) :
    conj (x œÜ) = x (ComplexEmbedding.conjugate œÜ) := by
  refine Submodule.span_induction hx ?_ ?_ (fun _ _ hx hy => ?_) (fun a _ hx => ?_)
  ¬∑ rintro _ ‚ü®x, rfl‚ü©
    rw [apply_at, apply_at, ComplexEmbedding.conjugate_coe_eq]
  ¬∑ rw [Pi.zero_apply, Pi.zero_apply, map_zero]
  ¬∑ rw [Pi.add_apply, Pi.add_apply, map_add, hx, hy]
  ¬∑ rw [Pi.smul_apply, Complex.real_smul, map_mul, Complex.conj_ofReal]
    exact congrArg ((a : ‚ÑÇ) * ¬∑) hx

theorem nnnorm_eq [NumberField K] (x : K) :
    ‚ÄñcanonicalEmbedding K x‚Äñ‚Çä = Finset.univ.sup (fun œÜ : K ‚Üí+* ‚ÑÇ => ‚ÄñœÜ x‚Äñ‚Çä) := by
  simp_rw [Pi.nnnorm_def, apply_at]

theorem norm_le_iff [NumberField K] (x : K) (r : ‚Ñù) :
    ‚ÄñcanonicalEmbedding K x‚Äñ ‚â§ r ‚Üî ‚àÄ œÜ : K ‚Üí+* ‚ÑÇ, ‚ÄñœÜ x‚Äñ ‚â§ r := by
  obtain hr | hr := lt_or_le r 0
  ¬∑ obtain ‚ü®œÜ‚ü© := (inferInstance : Nonempty (K ‚Üí+* ‚ÑÇ))
    refine iff_of_false ?_ ?_
    ¬∑ exact (hr.trans_le (norm_nonneg _)).not_le
    ¬∑ exact fun h => hr.not_le (le_trans (norm_nonneg _) (h œÜ))
  ¬∑ lift r to NNReal using hr
    simp_rw [‚Üê coe_nnnorm, nnnorm_eq, NNReal.coe_le_coe, Finset.sup_le_iff, Finset.mem_univ,
      forall_true_left]

variable (K)

/-- The image of `ùìû K` as a subring of `‚ÑÇ^n`. -/
def integerLattice : Subring ((K ‚Üí+* ‚ÑÇ) ‚Üí ‚ÑÇ) :=
  (RingHom.range (algebraMap (ùìû K) K)).map (canonicalEmbedding K)

theorem integerLattice.inter_ball_finite [NumberField K] (r : ‚Ñù) :
    ((integerLattice K : Set ((K ‚Üí+* ‚ÑÇ) ‚Üí ‚ÑÇ)) ‚à© Metric.closedBall 0 r).Finite := by
  obtain hr | _ := lt_or_le r 0
  ¬∑ simp [Metric.closedBall_eq_empty.2 hr]
  ¬∑ have heq : ‚àÄ x, canonicalEmbedding K x ‚àà Metric.closedBall 0 r ‚Üî
        ‚àÄ œÜ : K ‚Üí+* ‚ÑÇ, ‚ÄñœÜ x‚Äñ ‚â§ r := by
      intro x; rw [‚Üê norm_le_iff, mem_closedBall_zero_iff]
    convert (Embeddings.finite_of_norm_le K ‚ÑÇ r).image (canonicalEmbedding K)
    ext; constructor
    ¬∑ rintro ‚ü®‚ü®_, ‚ü®x, rfl‚ü©, rfl‚ü©, hx‚ü©
      exact ‚ü®x, ‚ü®SetLike.coe_mem x, fun œÜ => (heq _).mp hx œÜ‚ü©, rfl‚ü©
    ¬∑ rintro ‚ü®x, ‚ü®hx1, hx2‚ü©, rfl‚ü©
      exact ‚ü®‚ü®x, ‚ü®‚ü®x, hx1‚ü©, rfl‚ü©, rfl‚ü©, (heq x).mpr hx2‚ü©

open Module Fintype FiniteDimensional

/-- A `‚ÑÇ`-basis of `‚ÑÇ^n` that is also a `‚Ñ§`-basis of the `integerLattice`. -/
noncomputable def latticeBasis [NumberField K] :
    Basis (Free.ChooseBasisIndex ‚Ñ§ (ùìû K)) ‚ÑÇ ((K ‚Üí+* ‚ÑÇ) ‚Üí ‚ÑÇ) := by
  classical
  -- Let `B` be the canonical basis of `(K ‚Üí+* ‚ÑÇ) ‚Üí ‚ÑÇ`. We prove that the determinant of
  -- the image by `canonicalEmbedding` of the integral basis of `K` is nonzero. This
  -- will imply the result.
    let B := Pi.basisFun ‚ÑÇ (K ‚Üí+* ‚ÑÇ)
    let e : (K ‚Üí+* ‚ÑÇ) ‚âÉ Free.ChooseBasisIndex ‚Ñ§ (ùìû K) :=
      equivOfCardEq ((Embeddings.card K ‚ÑÇ).trans (finrank_eq_card_basis (integralBasis K)))
    let M := B.toMatrix (fun i => canonicalEmbedding K (integralBasis K (e i)))
    suffices M.det ‚â† 0 by
      rw [‚Üê isUnit_iff_ne_zero, ‚Üê Basis.det_apply, ‚Üê is_basis_iff_det] at this
      refine basisOfLinearIndependentOfCardEqFinrank
        ((linearIndependent_equiv e.symm).mpr this.1) ?_
      rw [‚Üê finrank_eq_card_chooseBasisIndex, RingOfIntegers.rank, finrank_fintype_fun_eq_card,
        Embeddings.card]
  -- In order to prove that the determinant is nonzero, we show that it is equal to the
  -- square of the discriminant of the integral basis and thus it is not zero
    let N := Algebra.embeddingsMatrixReindex ‚Ñö ‚ÑÇ (fun i => integralBasis K (e i))
      RingHom.equivRatAlgHom
    rw [show M = N.transpose by { ext:2; rfl }]
    rw [Matrix.det_transpose, ‚Üê pow_ne_zero_iff two_ne_zero]
    convert (map_ne_zero_iff _ (algebraMap ‚Ñö ‚ÑÇ).injective).mpr
      (Algebra.discr_not_zero_of_basis ‚Ñö (integralBasis K))
    rw [‚Üê Algebra.discr_reindex ‚Ñö (integralBasis K) e.symm]
    exact (Algebra.discr_eq_det_embeddingsMatrixReindex_pow_two ‚Ñö ‚ÑÇ
      (fun i => integralBasis K (e i)) RingHom.equivRatAlgHom).symm

@[simp]
theorem latticeBasis_apply [NumberField K] (i : Free.ChooseBasisIndex ‚Ñ§ (ùìû K)) :
    latticeBasis K i = (canonicalEmbedding K) (integralBasis K i) := by
  simp only [latticeBasis, integralBasis_apply, coe_basisOfLinearIndependentOfCardEqFinrank,
    Function.comp_apply, Equiv.apply_symm_apply]

theorem mem_span_latticeBasis [NumberField K] (x : (K ‚Üí+* ‚ÑÇ) ‚Üí ‚ÑÇ) :
    x ‚àà Submodule.span ‚Ñ§ (Set.range (latticeBasis K)) ‚Üî
      x ‚àà ((canonicalEmbedding K).comp (algebraMap (ùìû K) K)).range := by
  rw [show Set.range (latticeBasis K) =
      (canonicalEmbedding K).toIntAlgHom.toLinearMap '' (Set.range (integralBasis K)) by
    rw [‚Üê Set.range_comp]; exact congrArg Set.range (funext (fun i => latticeBasis_apply K i))]
  rw [‚Üê Submodule.map_span, ‚Üê SetLike.mem_coe, Submodule.map_coe]
  rw [‚Üê RingHom.map_range, Subring.mem_map, Set.mem_image]
  simp only [SetLike.mem_coe, mem_span_integralBasis K]
  rfl

end NumberField.canonicalEmbedding

namespace NumberField.mixedEmbedding

open NumberField NumberField.InfinitePlace FiniteDimensional Finset

/-- The space `‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ` with `(r‚ÇÅ, r‚ÇÇ)` the signature of `K`. -/
local notation "E" K =>
  ({w : InfinitePlace K // IsReal w} ‚Üí ‚Ñù) √ó ({w : InfinitePlace K // IsComplex w} ‚Üí ‚ÑÇ)

/-- The mixed embedding of a number field `K` of signature `(r‚ÇÅ, r‚ÇÇ)` into `‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ`. -/
noncomputable def _root_.NumberField.mixedEmbedding : K ‚Üí+* (E K) :=
  RingHom.prod (Pi.ringHom fun w => embedding_of_isReal w.prop)
    (Pi.ringHom fun w => w.val.embedding)

instance [NumberField K] : Nontrivial (E K) := by
  obtain ‚ü®w‚ü© := (inferInstance : Nonempty (InfinitePlace K))
  obtain hw | hw := w.isReal_or_isComplex
  ¬∑ have : Nonempty {w : InfinitePlace K // IsReal w} := ‚ü®‚ü®w, hw‚ü©‚ü©
    exact nontrivial_prod_left
  ¬∑ have : Nonempty {w : InfinitePlace K // IsComplex w} := ‚ü®‚ü®w, hw‚ü©‚ü©
    exact nontrivial_prod_right

protected theorem finrank [NumberField K] : finrank ‚Ñù (E K) = finrank ‚Ñö K := by
  classical
  rw [finrank_prod, finrank_pi, finrank_pi_fintype, Complex.finrank_real_complex, sum_const,
    card_univ, ‚Üê NrRealPlaces, ‚Üê NrComplexPlaces, ‚Üê card_real_embeddings, Algebra.id.smul_eq_mul,
    mul_comm, ‚Üê card_complex_embeddings, ‚Üê NumberField.Embeddings.card K ‚ÑÇ,
    Fintype.card_subtype_compl, Nat.add_sub_of_le (Fintype.card_subtype_le _)]

theorem _root_.NumberField.mixedEmbedding_injective [NumberField K] :
    Function.Injective (NumberField.mixedEmbedding K) := by
  exact RingHom.injective _

section commMap

/-- The linear map that makes `canonicalEmbedding` and `mixedEmbedding` commute, see
`commMap_canonical_eq_mixed`. -/
noncomputable def commMap : ((K ‚Üí+* ‚ÑÇ) ‚Üí ‚ÑÇ) ‚Üí‚Çó[‚Ñù] (E K) where
  toFun := fun x => ‚ü®fun w => (x w.val.embedding).re, fun w => x w.val.embedding‚ü©
  map_add' := by
    simp only [Pi.add_apply, Complex.add_re, Prod.mk_add_mk, Prod.mk.injEq]
    exact fun _ _ => ‚ü®rfl, rfl‚ü©
  map_smul' := by
    simp only [Pi.smul_apply, Complex.real_smul, Complex.mul_re, Complex.ofReal_re,
      Complex.ofReal_im, zero_mul, sub_zero, RingHom.id_apply, Prod.smul_mk, Prod.mk.injEq]
    exact fun _ _ => ‚ü®rfl, rfl‚ü©

theorem commMap_apply_of_isReal (x : (K ‚Üí+* ‚ÑÇ) ‚Üí ‚ÑÇ) {w : InfinitePlace K} (hw : IsReal w) :
    (commMap K x).1 ‚ü®w, hw‚ü© = (x w.embedding).re := rfl

theorem commMap_apply_of_isComplex (x : (K ‚Üí+* ‚ÑÇ) ‚Üí ‚ÑÇ) {w : InfinitePlace K} (hw : IsComplex w) :
    (commMap K x).2 ‚ü®w, hw‚ü© = x w.embedding := rfl

@[simp]
theorem commMap_canonical_eq_mixed (x : K) :
    commMap K (canonicalEmbedding K x) = mixedEmbedding K x := by
  simp only [canonicalEmbedding, commMap, LinearMap.coe_mk, AddHom.coe_mk, Pi.ringHom_apply,
    mixedEmbedding, RingHom.prod_apply, Prod.mk.injEq]
  exact ‚ü®rfl, rfl‚ü©

/-- This is a technical result to ensure that the image of the `‚ÑÇ`-basis of `‚ÑÇ^n` defined in
`canonicalEmbedding.latticeBasis` is a `‚Ñù`-basis of `‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ`,
see `mixedEmbedding.latticeBasis`. -/
theorem disjoint_span_commMap_ker [NumberField K] :
    Disjoint (Submodule.span ‚Ñù (Set.range (canonicalEmbedding.latticeBasis K)))
      (LinearMap.ker (commMap K)) := by
  refine LinearMap.disjoint_ker.mpr (fun x h_mem h_zero => ?_)
  replace h_mem : x ‚àà Submodule.span ‚Ñù (Set.range (canonicalEmbedding K)) := by
    refine (Submodule.span_mono ?_) h_mem
    rintro _ ‚ü®i, rfl‚ü©
    exact ‚ü®integralBasis K i, (canonicalEmbedding.latticeBasis_apply K i).symm‚ü©
  ext1 œÜ
  rw [Pi.zero_apply]
  by_cases hœÜ : ComplexEmbedding.IsReal œÜ
  ¬∑ apply Complex.ext
    ¬∑ rw [‚Üê embedding_mk_eq_of_isReal hœÜ, ‚Üê commMap_apply_of_isReal K x ‚ü®œÜ, hœÜ, rfl‚ü©]
      exact congrFun (congrArg (fun x => x.1) h_zero) ‚ü®InfinitePlace.mk œÜ, _‚ü©
    ¬∑ rw [Complex.zero_im, ‚Üê Complex.conj_eq_iff_im, canonicalEmbedding.conj_apply _ h_mem,
        ComplexEmbedding.isReal_iff.mp hœÜ]
  ¬∑ have := congrFun (congrArg (fun x => x.2) h_zero) ‚ü®InfinitePlace.mk œÜ, ‚ü®œÜ, hœÜ, rfl‚ü©‚ü©
    cases embedding_mk_eq œÜ with
    | inl h => rwa [‚Üê h, ‚Üê commMap_apply_of_isComplex K x ‚ü®œÜ, hœÜ, rfl‚ü©]
    | inr h =>
        apply RingHom.injective (starRingEnd ‚ÑÇ)
        rwa [canonicalEmbedding.conj_apply _ h_mem, ‚Üê h, map_zero,
          ‚Üê commMap_apply_of_isComplex K x ‚ü®œÜ, hœÜ, rfl‚ü©]

end commMap

noncomputable section norm

open scoped Classical

variable {K}

/-- The norm at the infinite place `w` of an element of
`({w // IsReal w} ‚Üí ‚Ñù) √ó ({ w // IsComplex w } ‚Üí ‚ÑÇ)`. -/
def normAtPlace (w : InfinitePlace K) : (E K) ‚Üí*‚ÇÄ ‚Ñù where
  toFun x := if hw : IsReal w then ‚Äñx.1 ‚ü®w, hw‚ü©‚Äñ else ‚Äñx.2 ‚ü®w, not_isReal_iff_isComplex.mp hw‚ü©‚Äñ
  map_zero' := by simp
  map_one' := by simp
  map_mul' x y := by split_ifs <;> simp

theorem normAtPlace_nonneg  (w : InfinitePlace K) (x : E K) :
    0 ‚â§ normAtPlace w x := by
  rw [normAtPlace, MonoidWithZeroHom.coe_mk, ZeroHom.coe_mk]
  split_ifs <;> exact norm_nonneg _

theorem normAtPlace_neg (w : InfinitePlace K) (x : E K)  :
    normAtPlace w (- x) = normAtPlace w x := by
  rw [normAtPlace, MonoidWithZeroHom.coe_mk, ZeroHom.coe_mk]
  split_ifs <;> simp

theorem normAtPlace_add_le (w : InfinitePlace K) (x y : E K) :
    normAtPlace w (x + y) ‚â§ normAtPlace w x + normAtPlace w y := by
  rw [normAtPlace, MonoidWithZeroHom.coe_mk, ZeroHom.coe_mk]
  split_ifs <;> exact norm_add_le _ _

theorem normAtPlace_smul (w : InfinitePlace K) (x : E K) (c : ‚Ñù) :
    normAtPlace w (c ‚Ä¢ x) = |c| * normAtPlace w x := by
  rw [normAtPlace, MonoidWithZeroHom.coe_mk, ZeroHom.coe_mk]
  split_ifs
  ¬∑ rw [Prod.smul_fst, Pi.smul_apply, norm_smul, Real.norm_eq_abs]
  ¬∑ rw [Prod.smul_snd, Pi.smul_apply, norm_smul, Real.norm_eq_abs, Complex.norm_eq_abs]

theorem normAtPlace_real (w : InfinitePlace K) (c : ‚Ñù) :
    normAtPlace w ((fun _ ‚Ü¶ c, fun _ ‚Ü¶ c) : (E K)) = |c| := by
  rw [show ((fun _ ‚Ü¶ c, fun _ ‚Ü¶ c) : (E K)) = c ‚Ä¢ 1 by ext <;> simp, normAtPlace_smul, map_one,
    mul_one]

theorem normAtPlace_apply_isReal {w : InfinitePlace K} (hw : IsReal w) (x : E K):
    normAtPlace w x = ‚Äñx.1 ‚ü®w, hw‚ü©‚Äñ := by
  rw [normAtPlace, MonoidWithZeroHom.coe_mk, ZeroHom.coe_mk, dif_pos]

theorem normAtPlace_apply_isComplex {w : InfinitePlace K} (hw : IsComplex w) (x : E K) :
    normAtPlace w x = ‚Äñx.2 ‚ü®w, hw‚ü©‚Äñ := by
  rw [normAtPlace, MonoidWithZeroHom.coe_mk, ZeroHom.coe_mk,
    dif_neg (not_isReal_iff_isComplex.mpr hw)]

@[simp]
theorem normAtPlace_apply (w : InfinitePlace K) (x : K) :
    normAtPlace w (mixedEmbedding K x) = w x := by
  simp_rw [normAtPlace, MonoidWithZeroHom.coe_mk, ZeroHom.coe_mk, mixedEmbedding,
    RingHom.prod_apply, Pi.ringHom_apply, norm_embedding_of_isReal, norm_embedding_eq, dite_eq_ite,
    ite_id]

theorem normAtPlace_eq_zero {x : E K} :
    (‚àÄ w, normAtPlace w x = 0) ‚Üî x = 0 := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ ext w
    ¬∑ exact norm_eq_zero'.mp (normAtPlace_apply_isReal w.prop _ ‚ñ∏ h w.1)
    ¬∑ exact norm_eq_zero'.mp (normAtPlace_apply_isComplex w.prop _ ‚ñ∏ h w.1)
  ¬∑ simp_rw [h, map_zero, implies_true]

variable [NumberField K]

theorem nnnorm_eq_sup_normAtPlace (x : E K) :
    ‚Äñx‚Äñ‚Çä = univ.sup fun w ‚Ü¶ ‚ü®normAtPlace w x, normAtPlace_nonneg w x‚ü© := by
  have :
      (univ : Finset (InfinitePlace K)) =
      (univ.image (fun w : {w : InfinitePlace K // IsReal w} ‚Ü¶ w.1)) ‚à™
      (univ.image (fun w : {w : InfinitePlace K // IsComplex w} ‚Ü¶ w.1)) := by
    ext; simp [isReal_or_isComplex]
  rw [this, sup_union, univ.sup_image, univ.sup_image, sup_eq_max,
    Prod.nnnorm_def', Pi.nnnorm_def, Pi.nnnorm_def]
  congr
  ¬∑ ext w
    simp [normAtPlace_apply_isReal w.prop]
  ¬∑ ext w
    simp [normAtPlace_apply_isComplex w.prop]

theorem norm_eq_sup'_normAtPlace (x : E K) :
    ‚Äñx‚Äñ = univ.sup' univ_nonempty fun w ‚Ü¶ normAtPlace w x := by
  rw [‚Üê coe_nnnorm, nnnorm_eq_sup_normAtPlace, ‚Üê sup'_eq_sup univ_nonempty, ‚Üê NNReal.val_eq_coe,
    ‚Üê OrderHom.Subtype.val_coe, map_finset_sup', OrderHom.Subtype.val_coe]
  simp only [Function.comp_apply]

/-- The norm of `x` is `‚àè w, (normAtPlace x) ^ mult w`. It is defined such that the norm of
`mixedEmbedding K a` for `a : K` is equal to the absolute value of the norm of `a` over `‚Ñö`,
see `norm_eq_norm`. -/
protected def norm : (E K) ‚Üí*‚ÇÄ ‚Ñù where
  toFun x := ‚àè w, (normAtPlace w x) ^ (mult w)
  map_one' := by simp only [map_one, one_pow, prod_const_one]
  map_zero' := by simp [mult]
  map_mul' _ _ := by simp only [map_mul, mul_pow, prod_mul_distrib]

protected theorem norm_apply (x : E K) :
    mixedEmbedding.norm x = ‚àè w, (normAtPlace w x) ^ (mult w) := rfl

protected theorem norm_nonneg (x : E K) :
    0 ‚â§ mixedEmbedding.norm x := univ.prod_nonneg fun _ _ ‚Ü¶ pow_nonneg (normAtPlace_nonneg _ _) _

protected theorem norm_eq_zero_iff {x : E K} :
    mixedEmbedding.norm x = 0 ‚Üî ‚àÉ w, normAtPlace w x = 0 := by
  simp_rw [mixedEmbedding.norm, MonoidWithZeroHom.coe_mk, ZeroHom.coe_mk, prod_eq_zero_iff,
    mem_univ, true_and, pow_eq_zero_iff mult_ne_zero]

protected theorem norm_ne_zero_iff {x : E K} :
    mixedEmbedding.norm x ‚â† 0 ‚Üî ‚àÄ w, normAtPlace w x ‚â† 0 := by
  rw [‚Üê not_iff_not]
  simp_rw [ne_eq, mixedEmbedding.norm_eq_zero_iff, not_not, not_forall, not_not]

theorem norm_smul (c : ‚Ñù) (x : E K) :
    mixedEmbedding.norm (c ‚Ä¢ x) = |c| ^ finrank ‚Ñö K * (mixedEmbedding.norm x) := by
  simp_rw [mixedEmbedding.norm_apply, normAtPlace_smul, mul_pow, prod_mul_distrib,
    prod_pow_eq_pow_sum, sum_mult_eq]

theorem norm_real (c : ‚Ñù) :
    mixedEmbedding.norm ((fun _ ‚Ü¶ c, fun _ ‚Ü¶ c) : (E K)) = |c| ^ finrank ‚Ñö K := by
  rw [show ((fun _ ‚Ü¶ c, fun _ ‚Ü¶ c) : (E K)) = c ‚Ä¢ 1 by ext <;> simp, norm_smul, map_one, mul_one]

@[simp]
theorem norm_eq_norm (x : K) :
    mixedEmbedding.norm (mixedEmbedding K x) = |Algebra.norm ‚Ñö x| := by
  simp_rw [mixedEmbedding.norm_apply, normAtPlace_apply, prod_eq_abs_norm]

theorem norm_eq_zero_iff' {x : E K} (hx : x ‚àà Set.range (mixedEmbedding K)) :
    mixedEmbedding.norm x = 0 ‚Üî x = 0 := by
  obtain ‚ü®a, rfl‚ü© := hx
  rw [norm_eq_norm, Rat.cast_abs, abs_eq_zero, Rat.cast_eq_zero, Algebra.norm_eq_zero_iff,
    map_eq_zero]

end norm

noncomputable section stdBasis

open scoped Classical

open Complex MeasureTheory MeasureTheory.Measure Zspan Matrix BigOperators Finset ComplexConjugate

variable [NumberField K]

/-- The type indexing the basis `stdBasis`. -/
abbrev index := {w : InfinitePlace K // IsReal w} ‚äï ({w : InfinitePlace K // IsComplex w}) √ó (Fin 2)

/-- The `‚Ñù`-basis of `({w // IsReal w} ‚Üí ‚Ñù) √ó ({ w // IsComplex w } ‚Üí ‚ÑÇ)` formed by the vector
equal to `1` at `w` and `0` elsewhere for `IsReal w` and by the couple of vectors equal to `1`
(resp. `I`) at `w` and `0` elsewhere for `IsComplex w`. -/
def stdBasis : Basis (index K) ‚Ñù (E K) :=
  Basis.prod (Pi.basisFun ‚Ñù _)
    (Basis.reindex (Pi.basis fun _ => basisOneI) (Equiv.sigmaEquivProd _ _))

variable {K}

@[simp]
theorem stdBasis_apply_ofIsReal (x : E K) (w : {w : InfinitePlace K // IsReal w}) :
    (stdBasis K).repr x (Sum.inl w) = x.1 w := rfl

@[simp]
theorem stdBasis_apply_ofIsComplex_fst (x : E K) (w : {w : InfinitePlace K // IsComplex w}) :
    (stdBasis K).repr x (Sum.inr ‚ü®w, 0‚ü©) = (x.2 w).re := rfl

@[simp]
theorem stdBasis_apply_ofIsComplex_snd (x : E K) (w : {w : InfinitePlace K // IsComplex w}) :
    (stdBasis K).repr x (Sum.inr ‚ü®w, 1‚ü©) = (x.2 w).im := rfl

variable (K)

theorem fundamentalDomain_stdBasis :
    fundamentalDomain (stdBasis K) =
        (Set.univ.pi fun _ => Set.Ico 0 1) √óÀ¢
        (Set.univ.pi fun _ => Complex.measurableEquivPi‚Åª¬π' (Set.univ.pi fun _ => Set.Ico 0 1)) := by
  ext
  simp [stdBasis, mem_fundamentalDomain, Complex.measurableEquivPi]

theorem volume_fundamentalDomain_stdBasis :
    volume (fundamentalDomain (stdBasis K)) = 1 := by
  rw [fundamentalDomain_stdBasis, volume_eq_prod, prod_prod, volume_pi, volume_pi, pi_pi, pi_pi,
    Complex.volume_preserving_equiv_pi.measure_preimage ?_, volume_pi, pi_pi, Real.volume_Ico,
    sub_zero, ENNReal.ofReal_one, prod_const_one, prod_const_one, prod_const_one, one_mul]
  exact MeasurableSet.pi Set.countable_univ (fun _ _ => measurableSet_Ico)

/-- The `Equiv` between `index K` and `K ‚Üí+* ‚ÑÇ` defined by sending a real infinite place `w` to
the unique corresponding embedding `w.embedding`, and the pair `‚ü®w, 0‚ü©` (resp. `‚ü®w, 1‚ü©`) for a
complex infinite place `w` to `w.embedding` (resp. `conjugate w.embedding`). -/
def indexEquiv : (index K) ‚âÉ (K ‚Üí+* ‚ÑÇ) := by
  refine Equiv.ofBijective (fun c => ?_)
    ((Fintype.bijective_iff_surjective_and_card _).mpr ‚ü®?_, ?_‚ü©)
  ¬∑ cases c with
    | inl w => exact w.val.embedding
    | inr wj => rcases wj with ‚ü®w, j‚ü©
                exact if j = 0 then w.val.embedding else ComplexEmbedding.conjugate w.val.embedding
  ¬∑ intro œÜ
    by_cases hœÜ : ComplexEmbedding.IsReal œÜ
    ¬∑ exact ‚ü®Sum.inl (InfinitePlace.mkReal ‚ü®œÜ, hœÜ‚ü©), by simp [embedding_mk_eq_of_isReal hœÜ]‚ü©
    ¬∑ by_cases hw : (InfinitePlace.mk œÜ).embedding = œÜ
      ¬∑ exact ‚ü®Sum.inr ‚ü®InfinitePlace.mkComplex ‚ü®œÜ, hœÜ‚ü©, 0‚ü©, by simp [hw]‚ü©
      ¬∑ exact ‚ü®Sum.inr ‚ü®InfinitePlace.mkComplex ‚ü®œÜ, hœÜ‚ü©, 1‚ü©,
          by simp [(embedding_mk_eq œÜ).resolve_left hw]‚ü©
  ¬∑ rw [Embeddings.card, ‚Üê mixedEmbedding.finrank K,
      ‚Üê FiniteDimensional.finrank_eq_card_basis (stdBasis K)]

variable {K}

@[simp]
theorem indexEquiv_apply_ofIsReal (w : {w : InfinitePlace K // IsReal w}) :
    (indexEquiv K) (Sum.inl w) = w.val.embedding := rfl

@[simp]
theorem indexEquiv_apply_ofIsComplex_fst (w : {w : InfinitePlace K // IsComplex w}) :
    (indexEquiv K) (Sum.inr ‚ü®w, 0‚ü©) = w.val.embedding := rfl

@[simp]
theorem indexEquiv_apply_ofIsComplex_snd (w : {w : InfinitePlace K // IsComplex w}) :
    (indexEquiv K) (Sum.inr ‚ü®w, 1‚ü©) = ComplexEmbedding.conjugate w.val.embedding := rfl

variable (K)

/-- The matrix that gives the representation on `stdBasis` of the image by `commMap` of an
element `x` of `(K ‚Üí+* ‚ÑÇ) ‚Üí ‚ÑÇ` fixed by the map `x_œÜ ‚Ü¶ conj x_(conjugate œÜ)`,
see `stdBasis_repr_eq_matrixToStdBasis_mul`. -/
def matrixToStdBasis : Matrix (index K) (index K) ‚ÑÇ :=
  fromBlocks (diagonal fun _ => 1) 0 0 <| reindex (Equiv.prodComm _ _) (Equiv.prodComm _ _)
    (blockDiagonal (fun _ => (2 : ‚ÑÇ)‚Åª¬π ‚Ä¢ !![1, 1; - I, I]))

theorem det_matrixToStdBasis :
    (matrixToStdBasis K).det = (2‚Åª¬π * I) ^ NrComplexPlaces K :=
  calc
  _ = ‚àè _k : { w : InfinitePlace K // IsComplex w }, det ((2 : ‚ÑÇ)‚Åª¬π ‚Ä¢ !![1, 1; -I, I]) := by
      rw [matrixToStdBasis, det_fromBlocks_zero‚ÇÇ‚ÇÅ, det_diagonal, prod_const_one, one_mul,
          det_reindex_self, det_blockDiagonal]
  _ = ‚àè _k : { w : InfinitePlace K // IsComplex w }, (2‚Åª¬π * Complex.I) := by
      refine prod_congr (Eq.refl _) (fun _ _ => ?_)
      field_simp; ring
  _ = (2‚Åª¬π * Complex.I) ^ Fintype.card {w : InfinitePlace K // IsComplex w} := by
      rw [prod_const, Fintype.card]

/-- Let `x : (K ‚Üí+* ‚ÑÇ) ‚Üí ‚ÑÇ` such that `x_œÜ = conj x_(conj œÜ)` for all `œÜ : K ‚Üí+* ‚ÑÇ`, then the
representation of `commMap K x` on `stdBasis` is given (up to reindexing) by the product of
`matrixToStdBasis` by `x`. -/
theorem stdBasis_repr_eq_matrixToStdBasis_mul (x : (K ‚Üí+* ‚ÑÇ) ‚Üí ‚ÑÇ)
    (hx : ‚àÄ œÜ, conj (x œÜ) = x (ComplexEmbedding.conjugate œÜ)) (c : index K) :
    ((stdBasis K).repr (commMap K x) c : ‚ÑÇ) =
      (matrixToStdBasis K *·µ• (x ‚àò (indexEquiv K))) c := by
  simp_rw [commMap, matrixToStdBasis, LinearMap.coe_mk, AddHom.coe_mk,
    mulVec, dotProduct, Function.comp_apply, index, Fintype.sum_sum_type,
    diagonal_one, reindex_apply, ‚Üê univ_product_univ, sum_product,
    indexEquiv_apply_ofIsReal, Fin.sum_univ_two, indexEquiv_apply_ofIsComplex_fst,
    indexEquiv_apply_ofIsComplex_snd, smul_of, smul_cons, smul_eq_mul,
    mul_one, Matrix.smul_empty, Equiv.prodComm_symm, Equiv.coe_prodComm]
  cases c with
  | inl w =>
      simp_rw [stdBasis_apply_ofIsReal, fromBlocks_apply‚ÇÅ‚ÇÅ, fromBlocks_apply‚ÇÅ‚ÇÇ,
        one_apply, Matrix.zero_apply, ite_mul, one_mul, zero_mul, sum_ite_eq, mem_univ, ite_true,
        add_zero, sum_const_zero, add_zero, ‚Üê conj_eq_iff_re, hx (embedding w.val),
        conjugate_embedding_eq_of_isReal w.prop]
  | inr c =>
    rcases c with ‚ü®w, j‚ü©
    fin_cases j
    ¬∑ simp_rw [Fin.mk_zero, stdBasis_apply_ofIsComplex_fst, fromBlocks_apply‚ÇÇ‚ÇÅ,
        fromBlocks_apply‚ÇÇ‚ÇÇ, Matrix.zero_apply, submatrix_apply,
        blockDiagonal_apply, Prod.swap_prod_mk, ite_mul, zero_mul, sum_const_zero, zero_add,
        sum_add_distrib, sum_ite_eq, mem_univ, ite_true, of_apply, cons_val', cons_val_zero,
        cons_val_one, head_cons, ‚Üê hx (embedding w), re_eq_add_conj]
      field_simp
    ¬∑ simp_rw [Fin.mk_one, stdBasis_apply_ofIsComplex_snd, fromBlocks_apply‚ÇÇ‚ÇÅ,
        fromBlocks_apply‚ÇÇ‚ÇÇ, Matrix.zero_apply, submatrix_apply, blockDiagonal_apply,
        Prod.swap_prod_mk, ite_mul, zero_mul, sum_const_zero, zero_add, sum_add_distrib, sum_ite_eq,
        mem_univ, ite_true, of_apply, cons_val', cons_val_zero, cons_val_one, head_cons,
        ‚Üê hx (embedding w), im_eq_sub_conj]
      ring_nf; field_simp

end stdBasis

noncomputable section integerLattice

variable [NumberField K]

open Module FiniteDimensional Module.Free

open scoped nonZeroDivisors

/-- A `‚Ñù`-basis of `‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ` that is also a `‚Ñ§`-basis of the image of `ùìû K`. -/
def latticeBasis :
    Basis (ChooseBasisIndex ‚Ñ§ (ùìû K)) ‚Ñù (E K) := by
  classical
    -- We construct an `‚Ñù`-linear independent family from the image of
    -- `canonicalEmbedding.lattice_basis` by `commMap`
    have := LinearIndependent.map (LinearIndependent.restrict_scalars
      (by { simpa only [Complex.real_smul, mul_one] using Complex.ofReal_injective })
      (canonicalEmbedding.latticeBasis K).linearIndependent)
      (disjoint_span_commMap_ker K)
    -- and it's a basis since it has the right cardinality
    refine basisOfLinearIndependentOfCardEqFinrank this ?_
    rw [‚Üê finrank_eq_card_chooseBasisIndex, RingOfIntegers.rank, finrank_prod, finrank_pi,
      finrank_pi_fintype, Complex.finrank_real_complex, sum_const, card_univ, ‚Üê NrRealPlaces,
      ‚Üê NrComplexPlaces, ‚Üê card_real_embeddings, Algebra.id.smul_eq_mul, mul_comm,
      ‚Üê card_complex_embeddings, ‚Üê NumberField.Embeddings.card K ‚ÑÇ, Fintype.card_subtype_compl,
      Nat.add_sub_of_le (Fintype.card_subtype_le _)]

@[simp]
theorem latticeBasis_apply (i : ChooseBasisIndex ‚Ñ§ (ùìû K)) :
    latticeBasis K i = (mixedEmbedding K) (integralBasis K i) := by
  simp only [latticeBasis, coe_basisOfLinearIndependentOfCardEqFinrank, Function.comp_apply,
    canonicalEmbedding.latticeBasis_apply, integralBasis_apply, commMap_canonical_eq_mixed]

theorem mem_span_latticeBasis (x : (E K)) :
    x ‚àà Submodule.span ‚Ñ§ (Set.range (latticeBasis K)) ‚Üî
      x ‚àà ((mixedEmbedding K).comp (algebraMap (ùìû K) K)).range := by
  rw [show Set.range (latticeBasis K) =
      (mixedEmbedding K).toIntAlgHom.toLinearMap '' (Set.range (integralBasis K)) by
    rw [‚Üê Set.range_comp]; exact congrArg Set.range (funext (fun i => latticeBasis_apply K i))]
  rw [‚Üê Submodule.map_span, ‚Üê SetLike.mem_coe, Submodule.map_coe]
  simp only [Set.mem_image, SetLike.mem_coe, mem_span_integralBasis K,
    RingHom.mem_range, exists_exists_eq_and]
  rfl

theorem mem_rat_span_latticeBasis (x : K) :
    mixedEmbedding K x ‚àà Submodule.span ‚Ñö (Set.range (latticeBasis K)) := by
  rw [‚Üê Basis.sum_repr (integralBasis K) x, map_sum]
  simp_rw [map_rat_smul]
  refine Submodule.sum_smul_mem _ _ (fun i _ ‚Ü¶ Submodule.subset_span ?_)
  rw [‚Üê latticeBasis_apply]
  exact Set.mem_range_self i

theorem latticeBasis_repr_apply (x : K) (i : ChooseBasisIndex ‚Ñ§ (ùìû K)) :
    (latticeBasis K).repr (mixedEmbedding K x) i = (integralBasis K).repr x i := by
  rw [‚Üê Basis.restrictScalars_repr_apply ‚Ñö _ ‚ü®_, mem_rat_span_latticeBasis K x‚ü©, eq_ratCast,
    Rat.cast_inj]
  let f := (mixedEmbedding K).toRatAlgHom.toLinearMap.codRestrict _
    (fun x ‚Ü¶ mem_rat_span_latticeBasis K x)
  suffices ((latticeBasis K).restrictScalars ‚Ñö).repr.toLinearMap ‚àò‚Çó f =
    (integralBasis K).repr.toLinearMap from DFunLike.congr_fun (LinearMap.congr_fun this x) i
  refine Basis.ext (integralBasis K) (fun i ‚Ü¶ ?_)
  have : f (integralBasis K i) = ((latticeBasis K).restrictScalars ‚Ñö) i := by
    apply Subtype.val_injective
    rw [LinearMap.codRestrict_apply, AlgHom.toLinearMap_apply, Basis.restrictScalars_apply,
      latticeBasis_apply]
    rfl
  simp_rw [LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply, this, Basis.repr_self]

variable (I : (FractionalIdeal (ùìû K)‚Å∞ K)À£)

/-- The generalized index of the lattice generated by `I` in the lattice generated by
`ùìû K` is equal to the norm of the ideal `I`. The result is stated in terms of base change
determinant and is the translation of `NumberField.det_basisOfFractionalIdeal_eq_absNorm` in
`‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ`. This is useful, in particular, to prove that the family obtained from
the `‚Ñ§`-basis of `I` is actually an `‚Ñù`-basis of `‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ`, see
`fractionalIdealLatticeBasis`. -/
theorem det_basisOfFractionalIdeal_eq_norm
    (e : (ChooseBasisIndex ‚Ñ§ (ùìû K)) ‚âÉ (ChooseBasisIndex ‚Ñ§ I)) :
    |Basis.det (latticeBasis K) ((mixedEmbedding K ‚àò (basisOfFractionalIdeal K I) ‚àò e))| =
      FractionalIdeal.absNorm I.1 := by
  suffices Basis.det (latticeBasis K) ((mixedEmbedding K ‚àò (basisOfFractionalIdeal K I) ‚àò e)) =
      (algebraMap ‚Ñö ‚Ñù) ((Basis.det (integralBasis K)) ((basisOfFractionalIdeal K I) ‚àò e)) by
    rw [this, eq_ratCast, ‚Üê Rat.cast_abs, ‚Üê Equiv.symm_symm e, ‚Üê Basis.coe_reindex,
      det_basisOfFractionalIdeal_eq_absNorm K I e]
  rw [Basis.det_apply, Basis.det_apply, RingHom.map_det]
  congr
  ext i j
  simp_rw [RingHom.mapMatrix_apply, Matrix.map_apply, Basis.toMatrix_apply, Function.comp_apply]
  exact latticeBasis_repr_apply K _ i

/-- A `‚Ñù`-basis of `‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ` that is also a `‚Ñ§`-basis of the image of the fractional
ideal `I`. -/
def fractionalIdealLatticeBasis :
    Basis (ChooseBasisIndex ‚Ñ§ I) ‚Ñù (E K) := by
  let e : (ChooseBasisIndex ‚Ñ§ (ùìû K)) ‚âÉ (ChooseBasisIndex ‚Ñ§ I) := by
    refine Fintype.equivOfCardEq ?_
    rw [‚Üê finrank_eq_card_chooseBasisIndex, ‚Üê finrank_eq_card_chooseBasisIndex,
      fractionalIdeal_rank]
  refine Basis.reindex ?_ e
  suffices IsUnit ((latticeBasis K).det ((mixedEmbedding K) ‚àò (basisOfFractionalIdeal K I) ‚àò e)) by
    rw [‚Üê is_basis_iff_det] at this
    exact Basis.mk this.1 (by rw [this.2])
  rw [isUnit_iff_ne_zero, ne_eq, ‚Üê abs_eq_zero.not, det_basisOfFractionalIdeal_eq_norm,
    Rat.cast_eq_zero, FractionalIdeal.absNorm_eq_zero_iff]
  exact Units.ne_zero I

@[simp]
theorem fractionalIdealLatticeBasis_apply (i : ChooseBasisIndex ‚Ñ§ I) :
    fractionalIdealLatticeBasis K I i = (mixedEmbedding K) (basisOfFractionalIdeal K I i) := by
  simp only [fractionalIdealLatticeBasis, Basis.coe_reindex, Basis.coe_mk, Function.comp_apply,
    Equiv.apply_symm_apply]

theorem mem_span_fractionalIdealLatticeBasis (x : (E K)) :
    x ‚àà Submodule.span ‚Ñ§ (Set.range (fractionalIdealLatticeBasis K I)) ‚Üî
      x ‚àà mixedEmbedding K '' I := by
  rw [show Set.range (fractionalIdealLatticeBasis K I) =
        (mixedEmbedding K).toIntAlgHom.toLinearMap '' (Set.range (basisOfFractionalIdeal K I)) by
      rw [‚Üê Set.range_comp]
      exact congr_arg Set.range (funext (fun i ‚Ü¶ fractionalIdealLatticeBasis_apply K I i))]
  rw [‚Üê Submodule.map_span, ‚Üê SetLike.mem_coe, Submodule.map_coe]
  rw [show Submodule.span ‚Ñ§ (Set.range (basisOfFractionalIdeal K I)) = (I : Set K) by
        ext; erw [mem_span_basisOfFractionalIdeal]]
  rfl

end integerLattice

end NumberField.mixedEmbedding
