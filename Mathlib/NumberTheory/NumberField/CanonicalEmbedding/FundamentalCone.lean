/-
Copyright (c) 2024 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.RingTheory.Ideal.IsPrincipal
import Mathlib.NumberTheory.NumberField.Units.DirichletTheorem
import Mathlib.RingTheory.ClassGroup

/-!
# Fundamental Cone

Let `K` be a number field of signature `(r‚ÇÅ, r‚ÇÇ)`. We define an action of the units `(ùìû K)À£` on
the mixed space `‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ` via the `mixedEmbedding`. The fundamental cone is a cone in the
mixed space that is a fundamental domain for the action of `(ùìû K)À£` modulo torsion.

## Main definitions and results

* `NumberField.mixedEmbedding.unitSMul`: the action of `(ùìû K)À£` on the mixed space defined, for
`u : (ùìû K)À£`, by multiplication component by component with `mixedEmbedding K u`.

* `NumberField.mixedEmbedding.fundamentalCone`: a cone in the mixed space, ie. a subset stable
  by multiplication by a nonzero real number, see `smul_mem_of_mem`, that is also a fundamental
  domain for the action of `(ùìû K)À£` modulo torsion, see `exists_unit_smul_mem` and
  `torsion_unit_smul_mem_of_mem`.

* `NumberField.mixedEmbedding.fundamentalCone.idealSet`: for `J` an integral ideal, the intersection
  between the fundamental cone and the `idealLattice` defined by the image of `J`.

* `NumberField.mixedEmbedding.fundamentalCone.idealSetEquivNorm`: for `J` an integral ideal and `n`
  a natural integer, the equivalence between the elements of `idealSet K` of norm `n` and the
  product of the set of nonzero principal ideals of `K` divisible by `J` of norm `n` and the
  torsion of `K`.

## Tags

number field, canonical embedding, units, principal ideals
-/

variable (K : Type*) [Field K]

namespace NumberField.mixedEmbedding

open NumberField NumberField.InfinitePlace

noncomputable section UnitSMul

/-- The action of `(ùìû K)À£` on the mixed space `‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ` defined, for `u : (ùìû K)À£`, by
multiplication component by component with `mixedEmbedding K u`. -/
@[simps]
instance unitSMul : SMul (ùìû K)À£ (mixedSpace K) where
  smul u x := mixedEmbedding K u * x

instance : MulAction (ùìû K)À£ (mixedSpace K) where
  one_smul := fun _ ‚Ü¶ by simp_rw [unitSMul_smul, Units.coe_one, map_one, one_mul]
  mul_smul := fun _ _ _ ‚Ü¶ by simp_rw [unitSMul_smul, Units.coe_mul, map_mul, mul_assoc]

instance : SMulZeroClass (ùìû K)À£ (mixedSpace K) where
  smul_zero := fun _ ‚Ü¶ by simp_rw [unitSMul_smul, mul_zero]

variable {K}

theorem unit_smul_eq_zero (u : (ùìû K)À£) (x : mixedSpace K) :
    u ‚Ä¢ x = 0 ‚Üî x = 0 := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ by rw [h, smul_zero]‚ü©
  contrapose! h
  obtain ‚ü®w, h‚ü© := exists_normAtPlace_ne_zero_iff.mpr h
  refine exists_normAtPlace_ne_zero_iff.mp ‚ü®w, ?_‚ü©
  rw [unitSMul_smul, map_mul]
  exact mul_ne_zero (by simp) h

variable [NumberField K]

theorem unit_smul_eq_iff_mul_eq {x y : ùìû K} {u : (ùìû K)À£} :
    u ‚Ä¢ mixedEmbedding K x = mixedEmbedding K y ‚Üî u * x = y := by
  rw [unitSMul_smul, ‚Üê map_mul, Function.Injective.eq_iff, ‚Üê RingOfIntegers.coe_eq_algebraMap,
    ‚Üê map_mul, ‚Üê RingOfIntegers.ext_iff]
  exact mixedEmbedding_injective K

theorem norm_unit_smul (u : (ùìû K)À£) (x : mixedSpace K) :
    mixedEmbedding.norm (u ‚Ä¢ x) = mixedEmbedding.norm x := by
  rw [unitSMul_smul, map_mul, norm_unit, one_mul]

end UnitSMul

noncomputable section logMap

open NumberField.Units NumberField.Units.dirichletUnitTheorem Module

variable [NumberField K] {K}

open Classical in
/-- The map from the mixed space to `logSpace K` defined in such way that: 1) it factors the map
`logEmbedding`, see `logMap_eq_logEmbedding`; 2) it is constant on the sets
`{c ‚Ä¢ x | c ‚àà ‚Ñù, c ‚â† 0}` if `norm x ‚â† 0`, see `logMap_real_smul`. -/
def logMap (x : mixedSpace K) : logSpace K := fun w ‚Ü¶
  mult w.val * (Real.log (normAtPlace w.val x) -
    Real.log (mixedEmbedding.norm x) * (finrank ‚Ñö K : ‚Ñù)‚Åª¬π)

@[simp]
theorem logMap_apply (x : mixedSpace K) (w : {w : InfinitePlace K // w ‚â† w‚ÇÄ}) :
    logMap x w = mult w.val * (Real.log (normAtPlace w.val x) -
      Real.log (mixedEmbedding.norm x) * (finrank ‚Ñö K : ‚Ñù)‚Åª¬π) := rfl

@[simp]
theorem logMap_zero : logMap (0 : mixedSpace K) = 0 := by
  ext; simp

@[simp]
theorem logMap_one : logMap (1 : mixedSpace K) = 0 := by
  ext; simp

variable {x y : mixedSpace K}

theorem logMap_mul (hx : mixedEmbedding.norm x ‚â† 0) (hy : mixedEmbedding.norm y ‚â† 0) :
    logMap (x * y) = logMap x + logMap y := by
  ext w
  simp_rw [Pi.add_apply, logMap_apply]
  rw [map_mul, map_mul, Real.log_mul, Real.log_mul hx hy, add_mul]
  ¬∑ ring
  ¬∑ exact mixedEmbedding.norm_ne_zero_iff.mp hx w
  ¬∑ exact mixedEmbedding.norm_ne_zero_iff.mp hy w

theorem logMap_apply_of_norm_one (hx : mixedEmbedding.norm x = 1)
    (w : {w : InfinitePlace K // w ‚â† w‚ÇÄ}) :
    logMap x w = mult w.val * Real.log (normAtPlace w x) := by
  rw [logMap_apply, hx, Real.log_one, zero_mul, sub_zero]

@[simp]
theorem logMap_eq_logEmbedding (u : (ùìû K)À£) :
    logMap (mixedEmbedding K u) = logEmbedding K (Additive.ofMul u) := by
  ext; simp

theorem logMap_unit_smul (u : (ùìû K)À£) (hx : mixedEmbedding.norm x ‚â† 0) :
    logMap (u ‚Ä¢ x) = logEmbedding K (Additive.ofMul u) + logMap x := by
  rw [unitSMul_smul, logMap_mul (by rw [norm_unit]; norm_num) hx, logMap_eq_logEmbedding]

variable (x) in
theorem logMap_torsion_smul {Œ∂ : (ùìû K)À£} (hŒ∂ : Œ∂ ‚àà torsion K) :
    logMap (Œ∂ ‚Ä¢ x) = logMap x := by
  ext
  simp_rw [logMap_apply, unitSMul_smul, map_mul, norm_eq_norm, Units.norm, Rat.cast_one, one_mul,
    normAtPlace_apply, (mem_torsion K).mp hŒ∂, one_mul]

theorem logMap_real (c : ‚Ñù) :
    logMap (c ‚Ä¢ (1 : mixedSpace K)) = 0 := by
  ext
  rw [logMap_apply, normAtPlace_smul, norm_smul, map_one, map_one, mul_one, mul_one, Real.log_pow,
    mul_comm (finrank ‚Ñö K : ‚Ñù) _, mul_assoc, mul_inv_cancel‚ÇÄ (Nat.cast_ne_zero.mpr finrank_pos.ne'),
    mul_one, sub_self, mul_zero, Pi.zero_apply]

theorem logMap_real_smul (hx : mixedEmbedding.norm x ‚â† 0) {c : ‚Ñù} (hc : c ‚â† 0) :
    logMap (c ‚Ä¢ x) = logMap x := by
  have : mixedEmbedding.norm (c ‚Ä¢ (1 : mixedSpace K)) ‚â† 0 := by
    rw [norm_smul, map_one, mul_one]
    exact pow_ne_zero _ (abs_ne_zero.mpr hc)
  rw [‚Üê smul_one_mul, logMap_mul this hx, logMap_real, zero_add]

theorem logMap_eq_of_normAtPlace_eq (h : ‚àÄ w, normAtPlace w x = normAtPlace w y) :
    logMap x = logMap y := by
  ext
  simp_rw [logMap_apply, h, norm_eq_of_normAtPlace_eq h]

end logMap

noncomputable section

open NumberField.Units NumberField.Units.dirichletUnitTheorem

variable [NumberField K]

open Classical in
/-- The fundamental cone is a cone in the mixed space, ie. a subset fixed by multiplication by
a nonzero real number, see `smul_mem_of_mem`, that is also a fundamental domain for the action
of `(ùìû K)À£` modulo torsion, see `exists_unit_smul_mem` and `torsion_smul_mem_of_mem`. -/
def fundamentalCone : Set (mixedSpace K) :=
  logMap‚Åª¬π' (ZSpan.fundamentalDomain ((basisUnitLattice K).ofZLatticeBasis ‚Ñù _)) \
      {x | mixedEmbedding.norm x = 0}

theorem measurableSet_fundamentalCone :
    MeasurableSet (fundamentalCone K) := by
  classical
  refine MeasurableSet.diff ?_ ?_
  ¬∑ unfold logMap
    refine MeasurableSet.preimage (ZSpan.fundamentalDomain_measurableSet _) <|
      measurable_pi_iff.mpr fun w ‚Ü¶ measurable_const.mul ?_
    exact (continuous_normAtPlace _).measurable.log.sub <|
      (mixedEmbedding.continuous_norm _).measurable.log.mul measurable_const
  ¬∑ exact measurableSet_eq_fun (mixedEmbedding.continuous_norm K).measurable measurable_const

namespace fundamentalCone

variable {K} {x y : mixedSpace K} {c : ‚Ñù}

theorem norm_pos_of_mem (hx : x ‚àà fundamentalCone K) :
    0 < mixedEmbedding.norm x :=
  lt_of_le_of_ne (mixedEmbedding.norm_nonneg _) (Ne.symm hx.2)

theorem normAtPlace_pos_of_mem (hx : x ‚àà fundamentalCone K) (w : InfinitePlace K) :
    0 < normAtPlace w x :=
  lt_of_le_of_ne (normAtPlace_nonneg _ _)
    (mixedEmbedding.norm_ne_zero_iff.mp (norm_pos_of_mem hx).ne' w).symm

theorem mem_of_normAtPlace_eq (hx : x ‚àà fundamentalCone K)
    (hy : ‚àÄ w, normAtPlace w y = normAtPlace w x) :
    y ‚àà fundamentalCone K := by
  refine ‚ü®?_, by simpa [norm_eq_of_normAtPlace_eq hy] using hx.2‚ü©
  rw [Set.mem_preimage, logMap_eq_of_normAtPlace_eq hy]
  exact hx.1

theorem smul_mem_of_mem (hx : x ‚àà fundamentalCone K) (hc : c ‚â† 0) :
    c ‚Ä¢ x ‚àà fundamentalCone K := by
  refine ‚ü®?_, ?_‚ü©
  ¬∑ rw [Set.mem_preimage, logMap_real_smul hx.2 hc]
    exact hx.1
  ¬∑ rw [Set.mem_setOf_eq, mixedEmbedding.norm_smul, mul_eq_zero, not_or]
    exact ‚ü®pow_ne_zero _ (abs_ne_zero.mpr hc), hx.2‚ü©

theorem smul_mem_iff_mem (hc : c ‚â† 0) :
    c ‚Ä¢ x ‚àà fundamentalCone K ‚Üî x ‚àà fundamentalCone K := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ smul_mem_of_mem h hc‚ü©
  convert smul_mem_of_mem h (inv_ne_zero hc)
  rw [eq_inv_smul_iff‚ÇÄ hc]

theorem exists_unit_smul_mem (hx : mixedEmbedding.norm x ‚â† 0) :
    ‚àÉ u : (ùìû K)À£, u ‚Ä¢ x ‚àà fundamentalCone K := by
  classical
  let B := (basisUnitLattice K).ofZLatticeBasis ‚Ñù
  rsuffices ‚ü®‚ü®_, ‚ü®u, _, rfl‚ü©‚ü©, hu‚ü© : ‚àÉ e : unitLattice K, e + logMap x ‚àà ZSpan.fundamentalDomain B
  ¬∑ exact ‚ü®u, by rwa [Set.mem_preimage, logMap_unit_smul u hx], by simp [hx]‚ü©
  ¬∑ obtain ‚ü®‚ü®e, h‚ÇÅ‚ü©, h‚ÇÇ, -‚ü© := ZSpan.exist_unique_vadd_mem_fundamentalDomain B (logMap x)
    exact ‚ü®‚ü®e, by rwa [‚Üê Basis.ofZLatticeBasis_span ‚Ñù (unitLattice K)]‚ü©, h‚ÇÇ‚ü©

theorem torsion_smul_mem_of_mem (hx : x ‚àà fundamentalCone K) {Œ∂ : (ùìû K)À£} (hŒ∂ : Œ∂ ‚àà torsion K) :
    Œ∂ ‚Ä¢ x ‚àà fundamentalCone K := by
  constructor
  ¬∑ rw [Set.mem_preimage, logMap_torsion_smul _ hŒ∂]
    exact hx.1
  ¬∑ rw [Set.mem_setOf_eq, unitSMul_smul, map_mul, norm_unit, one_mul]
    exact hx.2

theorem unit_smul_mem_iff_mem_torsion (hx : x ‚àà fundamentalCone K) (u : (ùìû K)À£) :
    u ‚Ä¢ x ‚àà fundamentalCone K ‚Üî u ‚àà torsion K := by
  classical
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ torsion_smul_mem_of_mem hx h‚ü©
  rw [‚Üê logEmbedding_eq_zero_iff]
  let B := (basisUnitLattice K).ofZLatticeBasis ‚Ñù
  refine (Subtype.mk_eq_mk (h := ?_) (h' := Submodule.zero_mem _)).mp <|
    (ZSpan.exist_unique_vadd_mem_fundamentalDomain B (logMap x)).unique ?_ ?_
  ¬∑ rw [Basis.ofZLatticeBasis_span ‚Ñù (unitLattice K)]
    exact ‚ü®u, trivial, rfl‚ü©
  ¬∑ rw [AddSubmonoid.mk_vadd, vadd_eq_add, ‚Üê logMap_unit_smul _ hx.2]
    exact h.1
  ¬∑ rw [AddSubmonoid.mk_vadd, vadd_eq_add, zero_add]
    exact hx.1

variable (K) in
/-- The intersection between the fundamental cone and the `integerLattice`. -/
def integerSet : Set (mixedSpace K) :=
  fundamentalCone K ‚à© mixedEmbedding.integerLattice K

theorem mem_integerSet {a : mixedSpace K} :
    a ‚àà integerSet K ‚Üî a ‚àà fundamentalCone K ‚àß ‚àÉ x : ùìû K, mixedEmbedding K x = a := by
  simp only [integerSet, Set.mem_inter_iff, SetLike.mem_coe, LinearMap.mem_range,
    AlgHom.toLinearMap_apply, RingHom.toIntAlgHom_coe, RingHom.coe_comp, Function.comp_apply]

/-- If `a` is in `integerSet`, then there is a *unique* algebraic integer in `ùìû K` such
that `mixedEmbedding K x = a`. -/
theorem existsUnique_preimage_of_mem_integerSet {a : mixedSpace K} (ha : a ‚àà integerSet K) :
    ‚àÉ! x : ùìû K, mixedEmbedding K x = a := by
  obtain ‚ü®_, ‚ü®x, rfl‚ü©‚ü© := mem_integerSet.mp ha
  refine Function.Injective.existsUnique_of_mem_range ?_ (Set.mem_range_self x)
  exact (mixedEmbedding_injective K).comp RingOfIntegers.coe_injective

@[deprecated (since := "2024-12-17")]
alias exists_unique_preimage_of_mem_integerSet := existsUnique_preimage_of_mem_integerSet

theorem ne_zero_of_mem_integerSet (a : integerSet K) : (a : mixedSpace K) ‚â† 0 := by
  by_contra!
  exact a.prop.1.2 (this.symm ‚ñ∏ mixedEmbedding.norm.map_zero')

open scoped nonZeroDivisors

/-- For `a : integerSet K`, the unique nonzero algebraic integer `x` such that its image by
`mixedEmbedding` is equal to `a`. Note that we state the fact that `x ‚â† 0` by saying that `x` is
a nonzero divisors since we will use later on the isomorphism
`Ideal.associatesNonZeroDivisorsEquivIsPrincipal`, see `integerSetEquiv`. -/
def preimageOfMemIntegerSet (a : integerSet K) : (ùìû K)‚Å∞ :=
  ‚ü®(mem_integerSet.mp a.prop).2.choose, mem_nonZeroDivisors_of_ne_zero (by
  simp_rw [ne_eq, ‚Üê RingOfIntegers.coe_injective.eq_iff, ‚Üê (mixedEmbedding_injective K).eq_iff,
    map_zero, (mem_integerSet.mp a.prop).2.choose_spec, ne_zero_of_mem_integerSet,
    not_false_eq_true])‚ü©

@[simp]
theorem mixedEmbedding_preimageOfMemIntegerSet (a : integerSet K) :
    mixedEmbedding K (preimageOfMemIntegerSet a : ùìû K) = (a : mixedSpace K) := by
  rw [preimageOfMemIntegerSet, (mem_integerSet.mp a.prop).2.choose_spec]

theorem preimageOfMemIntegerSet_mixedEmbedding {x : (ùìû K)}
    (hx : mixedEmbedding K (x : ùìû K) ‚àà integerSet K) :
    preimageOfMemIntegerSet (‚ü®mixedEmbedding K (x : ùìû K), hx‚ü©) = x := by
  simp_rw [RingOfIntegers.ext_iff, ‚Üê (mixedEmbedding_injective K).eq_iff,
    mixedEmbedding_preimageOfMemIntegerSet]

/-- If `x : mixedSpace K` is nonzero and the image of an algebraic integer, then there exists a
unit such that `u ‚Ä¢ x ‚àà integerSet K`. -/
theorem exists_unitSMul_mem_integerSet {x : mixedSpace K} (hx : x ‚â† 0)
    (hx' : x ‚àà mixedEmbedding K '' (Set.range (algebraMap (ùìû K) K))) :
    ‚àÉ u : (ùìû K)À£, u ‚Ä¢ x ‚àà integerSet K := by
  replace hx : mixedEmbedding.norm x ‚â† 0 :=
      (norm_eq_zero_iff' (Set.mem_range_of_mem_image (mixedEmbedding K) _ hx')).not.mpr hx
  obtain ‚ü®u, hu‚ü© := exists_unit_smul_mem hx
  obtain ‚ü®_, ‚ü®x, rfl‚ü©, _, rfl‚ü© := hx'
  exact ‚ü®u, mem_integerSet.mpr ‚ü®hu, u * x, by simp_rw [unitSMul_smul, ‚Üê map_mul]‚ü©‚ü©

/-- The set `integerSet K` is stable under the action of the torsion. -/
theorem torsion_unitSMul_mem_integerSet {x : mixedSpace K} {Œ∂ : (ùìû K)À£} (hŒ∂ : Œ∂ ‚àà torsion K)
    (hx : x ‚àà integerSet K) : Œ∂ ‚Ä¢ x ‚àà integerSet K := by
  obtain ‚ü®a, ‚ü®_, rfl‚ü©, rfl‚ü© := (mem_integerSet.mp hx).2
  refine mem_integerSet.mpr ‚ü®torsion_smul_mem_of_mem hx.1 hŒ∂, ‚ü®Œ∂ * a, by simp‚ü©‚ü©

/-- The action of `torsion K` on `integerSet K`. -/
@[simps]
instance integerSetTorsionSMul: SMul (torsion K) (integerSet K) where
  smul := fun ‚ü®Œ∂, hŒ∂‚ü© ‚ü®x, hx‚ü© ‚Ü¶ ‚ü®Œ∂ ‚Ä¢ x, torsion_unitSMul_mem_integerSet hŒ∂ hx‚ü©

instance : MulAction (torsion K) (integerSet K) where
  one_smul := fun _ ‚Ü¶ by
    rw [Subtype.mk_eq_mk, integerSetTorsionSMul_smul_coe, OneMemClass.coe_one, one_smul]
  mul_smul := fun _ _ _ ‚Ü¶ by
    rw [Subtype.mk_eq_mk]
    simp_rw [integerSetTorsionSMul_smul_coe, Subgroup.coe_mul, mul_smul]

/-- The `mixedEmbedding.norm` of `a : integerSet K` as a natural number, see also
`intNorm_coe`. -/
def intNorm (a : integerSet K) : ‚Ñï := (Algebra.norm ‚Ñ§ (preimageOfMemIntegerSet a : ùìû K)).natAbs

@[simp]
theorem intNorm_coe (a : integerSet K) :
    (intNorm a : ‚Ñù) = mixedEmbedding.norm (a : mixedSpace K) := by
  rw [intNorm, Int.cast_natAbs, ‚Üê Rat.cast_intCast, Int.cast_abs, Algebra.coe_norm_int,
    ‚Üê norm_eq_norm, mixedEmbedding_preimageOfMemIntegerSet]

/-- The norm `intNorm` lifts to a function on `integerSet K` modulo `torsion K`. -/
def quotIntNorm :
    Quotient (MulAction.orbitRel (torsion K) (integerSet K)) ‚Üí ‚Ñï :=
  Quotient.lift (fun x ‚Ü¶ intNorm x) fun a b ‚ü®u, hu‚ü© ‚Ü¶ by
    rw [‚Üê Nat.cast_inj (R := ‚Ñù), intNorm_coe, intNorm_coe, ‚Üê hu, integerSetTorsionSMul_smul_coe,
      norm_unit_smul]

@[simp]
theorem quotIntNorm_apply (a : integerSet K) : quotIntNorm ‚ü¶a‚üß = intNorm a := rfl

variable (K) in
/-- The map that sends an element of `a : integerSet K` to the associates class
of its preimage in `(ùìû K)‚Å∞`. By quotienting by the kernel of the map, which is equal to the
subgroup of torsion, we get the equivalence `integerSetQuotEquivAssociates`. -/
def integerSetToAssociates (a : integerSet K) : Associates (ùìû K)‚Å∞ :=
  ‚ü¶preimageOfMemIntegerSet a‚üß

@[simp]
theorem integerSetToAssociates_apply (a : integerSet K) :
    integerSetToAssociates K a = ‚ü¶preimageOfMemIntegerSet a‚üß := rfl

variable (K) in
theorem integerSetToAssociates_surjective :
    Function.Surjective (integerSetToAssociates K) := by
  rintro ‚ü®x‚ü©
  obtain ‚ü®u, hu‚ü© : ‚àÉ u : (ùìû K)À£, u ‚Ä¢ mixedEmbedding K (x : ùìû K) ‚àà integerSet K := by
    refine exists_unitSMul_mem_integerSet ?_ ‚ü®(x : ùìû K), Set.mem_range_self _, rfl‚ü©
    exact (map_ne_zero _).mpr <| RingOfIntegers.coe_ne_zero_iff.mpr (nonZeroDivisors.coe_ne_zero _)
  refine ‚ü®‚ü®u ‚Ä¢ mixedEmbedding K (x : ùìû K), hu‚ü©,
    Quotient.sound ‚ü®unitsNonZeroDivisorsEquiv.symm u‚Åª¬π, ?_‚ü©‚ü©
  simp_rw [Subtype.ext_iff, RingOfIntegers.ext_iff, ‚Üê (mixedEmbedding_injective K).eq_iff,
    Submonoid.coe_mul, map_mul, mixedEmbedding_preimageOfMemIntegerSet,
    unitSMul_smul, ‚Üê map_mul, mul_comm, map_inv, val_inv_unitsNonZeroDivisorsEquiv_symm_apply_coe,
    Units.mul_inv_cancel_right]

theorem integerSetToAssociates_eq_iff (a b : integerSet K) :
    integerSetToAssociates K a = integerSetToAssociates K b ‚Üî
      ‚àÉ Œ∂ : torsion K, Œ∂ ‚Ä¢ a = b := by
  simp_rw [integerSetToAssociates_apply, Associates.quotient_mk_eq_mk,
    Associates.mk_eq_mk_iff_associated, Associated, mul_comm, Subtype.ext_iff,
    RingOfIntegers.ext_iff, ‚Üê (mixedEmbedding_injective K).eq_iff, Submonoid.coe_mul, map_mul,
    mixedEmbedding_preimageOfMemIntegerSet, integerSetTorsionSMul_smul_coe]
  refine ‚ü®fun ‚ü®u, h‚ü© ‚Ü¶  ‚ü®‚ü®unitsNonZeroDivisorsEquiv u, ?_‚ü©, by simpa using h‚ü©,
    fun ‚ü®‚ü®u, _‚ü©, h‚ü© ‚Ü¶ ‚ü®unitsNonZeroDivisorsEquiv.symm u, by simpa using h‚ü©‚ü©
  exact (unit_smul_mem_iff_mem_torsion a.prop.1 _).mp (by simpa [h] using b.prop.1)

variable (K) in
/-- The equivalence between `integerSet K` modulo `torsion K` and `Associates (ùìû K)‚Å∞`. -/
def integerSetQuotEquivAssociates :
    Quotient (MulAction.orbitRel (torsion K) (integerSet K)) ‚âÉ Associates (ùìû K)‚Å∞ :=
  Equiv.ofBijective
    (Quotient.lift (integerSetToAssociates K)
      fun _ _ h ‚Ü¶ ((integerSetToAssociates_eq_iff _ _).mpr h).symm)
    ‚ü®by convert Setoid.ker_lift_injective (integerSetToAssociates K)
        all_goals
        ¬∑ ext a b
          rw [Setoid.ker_def, eq_comm, integerSetToAssociates_eq_iff b a,
            MulAction.orbitRel_apply, MulAction.mem_orbit_iff],
        (Quot.surjective_lift _).mpr (integerSetToAssociates_surjective K)‚ü©

@[simp]
theorem integerSetQuotEquivAssociates_apply (a : integerSet K) :
    integerSetQuotEquivAssociates K ‚ü¶a‚üß = ‚ü¶preimageOfMemIntegerSet a‚üß := rfl

theorem integerSetTorsionSMul_stabilizer (a : integerSet K) :
    MulAction.stabilizer (torsion K) a = ‚ä• := by
  refine (Subgroup.eq_bot_iff_forall _).mpr fun Œ∂ hŒ∂ ‚Ü¶ ?_
  rwa [MulAction.mem_stabilizer_iff, Subtype.ext_iff, integerSetTorsionSMul_smul_coe,
    unitSMul_smul, ‚Üê mixedEmbedding_preimageOfMemIntegerSet, ‚Üê map_mul,
    (mixedEmbedding_injective K).eq_iff, ‚Üê map_mul, ‚Üê RingOfIntegers.ext_iff, mul_eq_right‚ÇÄ,
    Units.val_eq_one, OneMemClass.coe_eq_one] at hŒ∂
  exact nonZeroDivisors.coe_ne_zero _

open Submodule Ideal

variable (K) in
/-- The equivalence between `integerSet K` and the product of the set of nonzero principal
ideals of `K` and the torsion of `K`. -/
def integerSetEquiv :
    integerSet K ‚âÉ {I : (Ideal (ùìû K))‚Å∞ // IsPrincipal I.val} √ó torsion K :=
  (MulAction.selfEquivSigmaOrbitsQuotientStabilizer (torsion K) (integerSet K)).trans
    ((Equiv.sigmaEquivProdOfEquiv (by
        intro _
        simp_rw [integerSetTorsionSMul_stabilizer]
        exact QuotientGroup.quotientBot.toEquiv)).trans
      (Equiv.prodCongrLeft (fun _ ‚Ü¶ (integerSetQuotEquivAssociates K).trans
        (Ideal.associatesNonZeroDivisorsEquivIsPrincipal (ùìû K)))))

@[simp]
theorem integerSetEquiv_apply_fst (a : integerSet K) :
    ((integerSetEquiv K a).1 : Ideal (ùìû K)) = span {(preimageOfMemIntegerSet a : ùìû K)} := rfl

variable (K) in
/-- For an integer `n`, The equivalence between the elements of `integerSet K` of norm `n`
and the product of the set of nonzero principal ideals of `K` of norm `n` and the torsion of `K`. -/
def integerSetEquivNorm (n : ‚Ñï) :
    {a : integerSet K // mixedEmbedding.norm (a : mixedSpace K) = n} ‚âÉ
      {I : (Ideal (ùìû K))‚Å∞ // IsPrincipal (I : Ideal (ùìû K)) ‚àß
        absNorm (I : Ideal (ùìû K)) = n} √ó (torsion K) :=
  calc
    _ ‚âÉ {I : {I : (Ideal (ùìû K))‚Å∞ // IsPrincipal I.1} √ó torsion K //
          absNorm (I.1 : Ideal (ùìû K)) = n} :=
      Equiv.subtypeEquiv (integerSetEquiv K) fun _ ‚Ü¶ by simp_rw [‚Üê intNorm_coe, intNorm,
        Nat.cast_inj, integerSetEquiv_apply_fst, absNorm_span_singleton]
    _ ‚âÉ {I : {I : (Ideal (ùìû K))‚Å∞ // IsPrincipal I.1} // absNorm (I.1 : Ideal (ùìû K)) = n} √ó
          torsion K := Equiv.prodSubtypeFstEquivSubtypeProd
      (p := fun I : {I : (Ideal (ùìû K))‚Å∞ // IsPrincipal I.1} ‚Ü¶ absNorm (I : Ideal (ùìû K)) = n)
    _ ‚âÉ {I : (Ideal (ùìû K))‚Å∞ // IsPrincipal (I : Ideal (ùìû K)) ‚àß
          absNorm (I : Ideal (ùìû K)) = n} √ó (torsion K) :=  Equiv.prodCongrLeft fun _ ‚Ü¶
      (Equiv.subtypeSubtypeEquivSubtypeInter
        (fun I : (Ideal (ùìû K))‚Å∞ ‚Ü¶ IsPrincipal I.1) (fun I ‚Ü¶ absNorm I.1 = n))

@[simp]
theorem integerSetEquivNorm_apply_fst {n : ‚Ñï}
    (a : {a : integerSet K // mixedEmbedding.norm (a : mixedSpace K) = n}) :
    ((integerSetEquivNorm K n a).1 : Ideal (ùìû K)) =
      span {(preimageOfMemIntegerSet a.val : ùìû K)} := by
  simp_rw [integerSetEquivNorm, Equiv.prodSubtypeFstEquivSubtypeProd, Equiv.trans_def,
    Equiv.prodCongrLeft, Equiv.trans_apply, Equiv.subtypeEquiv_apply, Equiv.coe_fn_mk,
    Equiv.subtypeSubtypeEquivSubtypeInter_apply_coe, integerSetEquiv_apply_fst]

variable (K)

/-- For `n` positive, the number of principal ideals in `ùìû K` of norm `n` multiplied by the order
of the torsion of `K` is equal to the number of elements in `integerSet K` of norm `n`. -/
theorem card_isPrincipal_norm_eq_mul_torsion (n : ‚Ñï) :
    Nat.card {I : (Ideal (ùìû K))‚Å∞ | IsPrincipal (I : Ideal (ùìû K)) ‚àß
      absNorm (I : Ideal (ùìû K)) = n} * torsionOrder K =
        Nat.card {a : integerSet K | mixedEmbedding.norm (a : mixedSpace K) = n} := by
  rw [torsionOrder, ‚Üê Nat.card_eq_fintype_card, ‚Üê Nat.card_prod]
  exact Nat.card_congr (integerSetEquivNorm K n).symm

variable (J : (Ideal (ùìû K))‚Å∞)

/-- The intersection between the fundamental cone and the `idealLattice` defined by the image of
the integral ideal `J`. -/
def idealSet : Set (mixedSpace K) :=
  fundamentalCone K ‚à© (mixedEmbedding.idealLattice K (FractionalIdeal.mk0 K J))

variable {K J} in
theorem mem_idealSet :
    x ‚àà idealSet K J ‚Üî x ‚àà fundamentalCone K ‚àß ‚àÉ a : (ùìû K), (a : ùìû K) ‚àà (J : Set (ùìû K)) ‚àß
      mixedEmbedding K (a : ùìû K) = x := by
  simp_rw [idealSet, Set.mem_inter_iff, idealLattice, SetLike.mem_coe, FractionalIdeal.coe_mk0,
    LinearMap.mem_range, LinearMap.coe_comp, LinearMap.coe_restrictScalars, coe_subtype,
    Function.comp_apply, AlgHom.toLinearMap_apply, RingHom.toIntAlgHom_coe, Subtype.exists,
    FractionalIdeal.mem_coe, FractionalIdeal.mem_coeIdeal, exists_prop', nonempty_prop,
    exists_exists_and_eq_and]

/-- The map that sends `a : idealSet` to an element of `integerSet`. This map exists because
`J` is an integral ideal. -/
def idealSetMap : idealSet K J ‚Üí integerSet K :=
  fun ‚ü®a, ha‚ü© ‚Ü¶ ‚ü®a, mem_integerSet.mpr ‚ü®(mem_idealSet.mp ha).1, (mem_idealSet.mp ha).2.choose,
    (mem_idealSet.mp ha).2.choose_spec.2‚ü©‚ü©

@[simp]
theorem idealSetMap_apply (a : idealSet K J) : (idealSetMap K J a : mixedSpace K) = a := rfl

theorem preimage_of_IdealSetMap (a : idealSet K J) :
    (preimageOfMemIntegerSet (idealSetMap K J a) : ùìû K) ‚àà (J : Set (ùìû K)) := by
  obtain ‚ü®_, ‚ü®x, hx‚ÇÅ, hx‚ÇÇ‚ü©‚ü© := mem_idealSet.mp a.prop
  simp_rw [idealSetMap, ‚Üê hx‚ÇÇ, preimageOfMemIntegerSet_mixedEmbedding]
  exact hx‚ÇÅ

/-- The map `idealSetMap` is actually an equiv between `idealSet K J` and the elements of
`integerSet K` whose preimage lies in `J`. -/
def idealSetEquiv : idealSet K J ‚âÉ
    {a : integerSet K | (preimageOfMemIntegerSet a : ùìû K) ‚àà (J : Set (ùìû K))} :=
  Equiv.ofBijective (fun a ‚Ü¶ ‚ü®idealSetMap K J a, preimage_of_IdealSetMap K J a‚ü©)
    ‚ü®fun _ _ h ‚Ü¶ (by
        simp_rw [Subtype.ext_iff_val, idealSetMap_apply] at h
        rwa [Subtype.ext_iff_val]),
    fun ‚ü®a, ha‚ÇÇ‚ü© ‚Ü¶ ‚ü®‚ü®a.val, mem_idealSet.mpr ‚ü®a.prop.1,
        ‚ü®preimageOfMemIntegerSet a, ha‚ÇÇ, mixedEmbedding_preimageOfMemIntegerSet a‚ü©‚ü©‚ü©, rfl‚ü©‚ü©

variable {K J}

theorem idealSetEquiv_apply (a : idealSet K J) :
    (idealSetEquiv K J a : mixedSpace K) = a := rfl

theorem idealSetEquiv_symm_apply
    (a : {a : integerSet K // (preimageOfMemIntegerSet a : ùìû K) ‚àà (J : Set (ùìû K)) }) :
    ((idealSetEquiv K J).symm a : mixedSpace K) = a := by
  rw [‚Üê (idealSetEquiv_apply ((idealSetEquiv K J).symm a)), Equiv.apply_symm_apply]

theorem intNorm_idealSetEquiv_apply (a : idealSet K J) :
    intNorm (idealSetEquiv K J a).val = mixedEmbedding.norm (a : mixedSpace K) := by
  rw [intNorm_coe, idealSetEquiv_apply]

variable (K J)

/-- For an integer `n`, The equivalence between the elements of `idealSet K` of norm `n` and
the product of the set of nonzero principal ideals of `K` divisible by `J` of norm `n` and the
torsion of `K`. -/
def idealSetEquivNorm (n : ‚Ñï) :
    {a : idealSet K J // mixedEmbedding.norm (a : mixedSpace K) = n} ‚âÉ
      {I : (Ideal (ùìû K))‚Å∞ // (J : Ideal (ùìû K)) ‚à£ I ‚àß IsPrincipal (I : Ideal (ùìû K)) ‚àß
        absNorm (I : Ideal (ùìû K)) = n} √ó (torsion K) :=
  calc
    _ ‚âÉ {a : {a : integerSet K // (preimageOfMemIntegerSet a).1 ‚àà J.1} //
            mixedEmbedding.norm a.1.1 = n} := by
        convert (Equiv.subtypeEquivOfSubtype (idealSetEquiv K J).symm).symm using 3
        rw [idealSetEquiv_symm_apply]
    _ ‚âÉ {a : integerSet K // (preimageOfMemIntegerSet a).1 ‚àà J.1 ‚àß
          mixedEmbedding.norm a.1 = n} := Equiv.subtypeSubtypeEquivSubtypeInter
        (fun a : integerSet K ‚Ü¶ (preimageOfMemIntegerSet a).1 ‚àà J.1)
        (fun a ‚Ü¶ mixedEmbedding.norm a.1 = n)
    _ ‚âÉ {a : {a :integerSet K // mixedEmbedding.norm a.1 = n} //
          (preimageOfMemIntegerSet a.1).1 ‚àà J.1} := ((Equiv.subtypeSubtypeEquivSubtypeInter
        (fun a : integerSet K ‚Ü¶ mixedEmbedding.norm a.1 = n)
        (fun a ‚Ü¶ (preimageOfMemIntegerSet a).1 ‚àà J.1)).trans
        (Equiv.subtypeEquivRight (fun _ ‚Ü¶ by simp [and_comm]))).symm
    _ ‚âÉ {I : {I : (Ideal (ùìû K))‚Å∞ // IsPrincipal I.1 ‚àß absNorm I.1 = n} √ó (torsion K) //
          J.1 ‚à£ I.1.1} := by
      convert Equiv.subtypeEquivOfSubtype (p := fun I ‚Ü¶ J.1 ‚à£ I.1) (integerSetEquivNorm K n)
      rw [integerSetEquivNorm_apply_fst, dvd_span_singleton]
    _ ‚âÉ {I : {I : (Ideal (ùìû K))‚Å∞ // IsPrincipal I.1 ‚àß absNorm I.1 = n} // J.1 ‚à£ I.1} √ó
        (torsion K) := Equiv.prodSubtypeFstEquivSubtypeProd
        (p := fun I : {I : (Ideal (ùìû K))‚Å∞ // IsPrincipal I.1 ‚àß absNorm I.1 = n} ‚Ü¶ J.1 ‚à£ I.1)
    _ ‚âÉ {I : (Ideal (ùìû K))‚Å∞ // (IsPrincipal I.1 ‚àß absNorm I.1 = n) ‚àß J.1 ‚à£ I.1} √ó (torsion K) :=
      Equiv.prodCongrLeft fun _ ‚Ü¶ (Equiv.subtypeSubtypeEquivSubtypeInter
        (fun I : (Ideal (ùìû K))‚Å∞ ‚Ü¶ IsPrincipal I.1 ‚àß absNorm I.1 = n)
        (fun I ‚Ü¶ J.1 ‚à£ I.1))
    _ ‚âÉ {I : (Ideal (ùìû K))‚Å∞ // J.1 ‚à£ I.1 ‚àß IsPrincipal I.1 ‚àß absNorm I.1 = n} √ó
          (Units.torsion K) :=
      Equiv.prodCongrLeft fun _ ‚Ü¶ Equiv.subtypeEquivRight fun _ ‚Ü¶ by rw [and_comm]

/-- For `s : ‚Ñù`, the number of principal nonzero ideals in `ùìû K` divisible par `J` of norm `‚â§ s`
multiplied by the order of the torsion of `K` is equal to the number of elements in `idealSet K J`
of norm `‚â§ s`. -/
theorem card_isPrincipal_dvd_norm_le (s : ‚Ñù) :
    Nat.card {I : (Ideal (ùìû K))‚Å∞ // (J : Ideal (ùìû K)) ‚à£ I ‚àß IsPrincipal (I : Ideal (ùìû K)) ‚àß
      absNorm (I : Ideal (ùìû K)) ‚â§ s} * torsionOrder K =
        Nat.card {a : idealSet K J // mixedEmbedding.norm (a : mixedSpace K) ‚â§ s} := by
  obtain hs | hs := le_or_gt 0 s
  ¬∑ simp_rw [‚Üê intNorm_idealSetEquiv_apply, ‚Üê Nat.le_floor_iff hs]
    rw [torsionOrder, ‚Üê Nat.card_eq_fintype_card, ‚Üê Nat.card_prod]
    refine Nat.card_congr <| @Equiv.ofFiberEquiv _ (Œ≥ := Finset.Iic ‚åäs‚åã‚Çä) _
      (fun I ‚Ü¶ ‚ü®absNorm I.1.val.1, Finset.mem_Iic.mpr I.1.prop.2.2‚ü©)
      (fun a ‚Ü¶ ‚ü®intNorm (idealSetEquiv K J a.1).1, Finset.mem_Iic.mpr a.prop‚ü©) fun ‚ü®i, hi‚ü© ‚Ü¶ ?_
    simp_rw [Subtype.mk.injEq]
    calc _ ‚âÉ {I : {I : (Ideal (ùìû K))‚Å∞ // _ ‚àß _ ‚àß _} // absNorm I.1.1 = i} √ó torsion K :=
        Equiv.prodSubtypeFstEquivSubtypeProd
      _    ‚âÉ {I : (Ideal (ùìû K))‚Å∞ // (_ ‚àß _ ‚àß absNorm I.1 ‚â§ ‚åäs‚åã‚Çä) ‚àß absNorm I.1 = i}
            √ó torsion K := Equiv.prodCongrLeft fun _ ‚Ü¶ (Equiv.subtypeSubtypeEquivSubtypeInter
        (p := fun I : (Ideal (ùìû K))‚Å∞ ‚Ü¶ J.1 ‚à£ I.1 ‚àß IsPrincipal I.1 ‚àß absNorm I.1 ‚â§ ‚åäs‚åã‚Çä)
        (q := fun I ‚Ü¶ absNorm I.1 = i))
      _   ‚âÉ {I : (Ideal (ùìû K))‚Å∞ // J.1 ‚à£ I.1 ‚àß IsPrincipal I.1 ‚àß absNorm I.1 = i}
            √ó torsion K := Equiv.prodCongrLeft fun _ ‚Ü¶ Equiv.subtypeEquivRight fun _ ‚Ü¶ by aesop
      _   ‚âÉ {a : idealSet K J // mixedEmbedding.norm (a : mixedSpace K) = i} :=
            (idealSetEquivNorm K J i).symm
      _   ‚âÉ {a : idealSet K J // intNorm (idealSetEquiv K J a).1 = i} := by
        simp_rw [‚Üê intNorm_idealSetEquiv_apply, Nat.cast_inj]
        rfl
      _   ‚âÉ {b : {a : idealSet K J // intNorm (idealSetEquiv K J a).1 ‚â§ ‚åäs‚åã‚Çä} //
            intNorm (idealSetEquiv K J b).1 = i} :=
        (Equiv.subtypeSubtypeEquivSubtype fun h ‚Ü¶ Finset.mem_Iic.mp (h ‚ñ∏ hi)).symm
  ¬∑ simp_rw [lt_iff_not_ge.mp (lt_of_lt_of_le hs (Nat.cast_nonneg _)), lt_iff_not_ge.mp
      (lt_of_lt_of_le hs (mixedEmbedding.norm_nonneg _)), and_false, Nat.card_of_isEmpty,
      zero_mul]

end fundamentalCone

end

end NumberField.mixedEmbedding
