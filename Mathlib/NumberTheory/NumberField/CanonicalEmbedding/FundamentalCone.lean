/-
Copyright (c) 2024 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.NumberTheory.NumberField.Units

/-!
# Fundamental Cone

Let `K` be a number field of signature `(râ‚, râ‚‚)`. We define an action of the units `(ğ“ K)Ë£` on
the space `â„^râ‚ Ã— â„‚^râ‚‚`. The fundamental cone is a cone in `â„^râ‚ Ã— â„‚^râ‚‚` that is a fundamental
domain for the action of `(ğ“ K)Ë£` up to roots of unity.

## Main definitions and results

* `NumberField.mixedEmbedding.unitSMul`: the action of `(ğ“ K)Ë£` on `â„^râ‚ Ã— â„‚^râ‚‚` defined, for
`u : (ğ“ K)Ë£`, by multiplication component by component with `mixedEmbedding K u`.

* `NumberField.mixedEmbedding.fundamentalCone`: a cone in `â„^râ‚ Ã— â„‚^râ‚‚` --that is a subset fixed
by multiplication by a scalar, see `smul_mem_of_mem`--, that is also a fundamental domain for the
action of `(ğ“ K)Ë£` up to roots of unity, see `exists_unitSMul_me` and
`torsion_unitSMul_mem_of_mem`.

* `NumberField.mixedEmbedding.fundamentalCone.integralPoint`: the subset of elements of the
fundamental cone that are images by `mixedEmbedding` of algebraic integers of `K`.

* `NumberField.mixedEmbedding.fundamentalCone.integralPointQuotEquivIsPrincipal`: the equivalence
between `fundamentalCone.integralPoint K / torsion K` and the principal ideals of `ğ“ K`.

* `NumberField.mixedEmbedding.fundamentalCone.card_isPrincipal_norm_mul`: for `n` positive, the
number of principal ideals in `ğ“ K` of norm `n` multiplied by the number of roots of unity is
equal to the number of `fundamentalCone.integralPoint K` of norm `n`.

## Tags

number field, canonical embedding, principal ideals
-/

variable (K : Type*) [Field K]

namespace NumberField.mixedEmbedding

open NumberField NumberField.InfinitePlace

/-- The space `â„^râ‚ Ã— â„‚^râ‚‚` with `(râ‚, râ‚‚)` the signature of `K`. -/
local notation "E" K =>
  ({w : InfinitePlace K // IsReal w} â†’ â„) Ã— ({w : InfinitePlace K // IsComplex w} â†’ â„‚)

noncomputable section UnitSMul

/-- The action of `(ğ“ K)Ë£` on `â„^râ‚ Ã— â„‚^râ‚‚` defined, for `u : (ğ“ K)Ë£`, by multiplication components
by components with `mixedEmbedding K u`. -/
@[simps]
instance unitSMul : SMul (ğ“ K)Ë£ (E K) where
  smul := fun u x â†¦ (mixedEmbedding K u) * x

instance : MulAction (ğ“ K)Ë£ (E K) where
  one_smul := fun _ â†¦ by simp_rw [HSMul.hSMul, SMul.smul, Units.coe_one, map_one, one_mul]
  mul_smul := fun _ _ _ â†¦ by simp_rw [HSMul.hSMul, SMul.smul, Units.coe_mul, map_mul, mul_assoc]

instance : SMulZeroClass (ğ“ K)Ë£ (E K) where
  smul_zero := fun _ â†¦ by simp_rw [HSMul.hSMul, SMul.smul, mul_zero]

variable [NumberField K]

theorem unitSMul_eq_iff_mul_eq {x y : (ğ“ K)} {u : (ğ“ K)Ë£} :
    u â€¢ mixedEmbedding K x = mixedEmbedding K y â†” u * x = y := by
  rw [unitSMul_smul, â† map_mul, Function.Injective.eq_iff, â† Submonoid.coe_mul, Subtype.mk_eq_mk]
  exact mixedEmbedding_injective K

theorem norm_unit (u : (ğ“ K)Ë£) :
    mixedEmbedding.norm (mixedEmbedding K u) = 1 := by
  rw [norm_eq_norm, show |(Algebra.norm â„š) (u : K)| = 1
      by exact NumberField.isUnit_iff_norm.mp (Units.isUnit u), Rat.cast_one]

theorem norm_unitSMul (u : (ğ“ K)Ë£) (x : E K) :
    mixedEmbedding.norm (u â€¢ x) = mixedEmbedding.norm x := by
  rw [unitSMul_smul, map_mul, norm_unit, one_mul]

end UnitSMul

noncomputable section logMap

open NumberField.Units NumberField.Units.dirichletUnitTheorem FiniteDimensional

variable [NumberField K] {K}

/-- The map from `â„^râ‚ Ã— â„‚^râ‚‚` to `{w : InfinitePlace K // w â‰  wâ‚€} â†’ â„` (with `wâ‚€` a fixed place)
define in such way that: 1) it factors the map `logEmbedding`, see `logMap_eq_logEmbedding`;
2) it is constant on the lines `{c â€¢ x | c âˆˆ â„}`, see `logMap_smul`. -/
def logMap (x : E K) : {w : InfinitePlace K // w â‰  wâ‚€} â†’ â„ := by
  classical
  exact fun w â†¦
    if hw : IsReal w.val then
      Real.log â€–x.1 âŸ¨w.val, hwâŸ©â€– - Real.log (mixedEmbedding.norm x) * (finrank â„š K : â„)â»Â¹
    else
      2 * (Real.log â€–x.2 âŸ¨w.val, not_isReal_iff_isComplex.mp hwâŸ©â€– -
        Real.log (mixedEmbedding.norm x) * (finrank â„š K : â„)â»Â¹)

theorem logMap_zero : logMap (0 : E K) = 0 := by
  ext
  simp_rw [logMap, Prod.fst_zero, Prod.snd_zero, map_zero, Pi.zero_apply, norm_zero, Real.log_zero,
    zero_mul, sub_zero, mul_zero, dite_eq_ite, ite_self]

theorem logMap_mul {x y : E K} (hx : mixedEmbedding.norm x â‰  0) (hy : mixedEmbedding.norm y â‰  0) :
    logMap (x * y) = logMap x + logMap y := by
  ext w
  simp_rw [Pi.add_apply, logMap]
  split_ifs with hw
  Â· rw [Prod.fst_mul, Pi.mul_apply, norm_mul, map_mul, Real.log_mul, Real.log_mul hx hy, add_mul]
    Â· ring
    Â· exact norm_ne_zero_iff.mpr <| (mixedEmbedding.norm_ne_zero_iff.mp hx).1 âŸ¨_, hwâŸ©
    Â· exact norm_ne_zero_iff.mpr <| (mixedEmbedding.norm_ne_zero_iff.mp hy).1 âŸ¨_, hwâŸ©
  Â· replace hw := not_isReal_iff_isComplex.mp hw
    rw [Prod.snd_mul, Pi.mul_apply, norm_mul, map_mul, Real.log_mul, Real.log_mul hx hy, add_mul]
    Â· ring
    Â· exact norm_ne_zero_iff.mpr <| (mixedEmbedding.norm_ne_zero_iff.mp hx).2 âŸ¨_, hwâŸ©
    Â· exact norm_ne_zero_iff.mpr <| (mixedEmbedding.norm_ne_zero_iff.mp hy).2 âŸ¨_, hwâŸ©

theorem logMap_eq_logEmbedding (u : (ğ“ K)Ë£) :
    logMap (mixedEmbedding K u) = logEmbedding K u := by
  ext
  simp_rw [logMap, mixedEmbedding.norm_unit, Real.log_one, zero_mul, sub_zero, logEmbedding,
    AddMonoidHom.coe_mk, ZeroHom.coe_mk, mult, Nat.cast_ite, ite_mul, Nat.cast_one, one_mul,
    Nat.cast_ofNat, mixedEmbedding, RingHom.prod_apply, Pi.ringHom_apply, norm_embedding_eq,
    norm_embedding_of_isReal]
  rfl

theorem logMap_unitSMul (u : (ğ“ K)Ë£) {x : E K} (hx : mixedEmbedding.norm x â‰  0) :
    logMap (u â€¢ x) = logEmbedding K u + logMap x := by
  rw [unitSMul_smul, logMap_mul (by rw [norm_unit]; norm_num) hx, logMap_eq_logEmbedding]

theorem logMap_torsion_unitSMul (x : E K) {Î¶ : (ğ“ K)Ë£} (hÎ¶ : Î¶ âˆˆ torsion K) :
    logMap (Î¶ â€¢ x) = logMap x := by
  ext
  simp_rw [logMap, unitSMul_smul, Prod.fst_mul, Prod.snd_mul, Pi.mul_apply, norm_mul, map_mul,
    norm_eq_norm, mixedEmbedding, RingHom.prod_apply, Pi.ringHom_apply,
    show |(Algebra.norm â„š) (Î¶ : K)| = 1 by exact NumberField.isUnit_iff_norm.mp (Units.isUnit Î¶),
    Rat.cast_one, one_mul, norm_embedding_eq, norm_embedding_of_isReal, (mem_torsion K).mp hÎ¶,
    one_mul]

theorem logMap_smul {x : E K} {c : â„} (hx : mixedEmbedding.norm x â‰  0) (hc : c â‰  0) :
    logMap (c â€¢ x) = logMap x := by
  rw [show c â€¢ x = ((fun _ â†¦ c, fun _ â†¦ c) : (E K)) * x by rfl, logMap_mul _ hx, add_left_eq_self]
  ext
  have hr : (finrank â„š K : â„) â‰  0 :=  Nat.cast_ne_zero.mpr (ne_of_gt finrank_pos)
  simp_rw [logMap, Pi.zero_apply, norm_real, Real.log_pow, mul_comm, inv_mul_cancel_leftâ‚€ hr,
    Real.norm_eq_abs, Complex.norm_eq_abs,  Complex.abs_ofReal, sub_self, mul_zero, dite_eq_ite,
    ite_self]
  rw [norm_real]
  exact pow_ne_zero _ (abs_ne_zero.mpr hc)

end logMap

noncomputable section

open NumberField.Units NumberField.Units.dirichletUnitTheorem

variable [NumberField K]

/-- The fundamental cone is a cone in `â„^râ‚ Ã— â„‚^râ‚‚` --that is a subset fixed by multiplication by
a scalar, see `smul_mem_of_mem`--, that is also a fundamental domain for the action of `(ğ“ K)Ë£` up
to roots of unity, see `exists_unitSMul_mem` and `torsion_unitSMul_mem_of_mem`. -/
def fundamentalCone : Set (E K) := by
  classical
  let B := (Module.Free.chooseBasis â„¤ (unitLattice K)).ofZlatticeBasis â„ _
  exact logMapâ»Â¹' (Zspan.fundamentalDomain B)

namespace fundamentalCone

protected theorem zero_mem : 0 âˆˆ fundamentalCone K := by
  simp_rw [fundamentalCone, Set.mem_preimage, Zspan.mem_fundamentalDomain, logMap_zero, map_zero,
    Finsupp.coe_zero, Pi.zero_apply, Set.left_mem_Ico, zero_lt_one, implies_true]

variable {K}

theorem smul_mem_of_mem {x : E K} (hx : mixedEmbedding.norm x â‰  0) (hx' : x âˆˆ fundamentalCone K)
    (c : â„) : c â€¢ x âˆˆ fundamentalCone K := by
  by_cases hc : c = 0
  Â· rw [hc, zero_smul]
    exact fundamentalCone.zero_mem K
  Â· rwa [fundamentalCone, Set.mem_preimage, logMap_smul hx hc]

theorem exists_unitSMul_mem {x : E K} (hx : mixedEmbedding.norm x â‰  0) :
    âˆƒ u : (ğ“ K)Ë£, u â€¢ x âˆˆ fundamentalCone K := by
  classical
  let B := (Module.Free.chooseBasis â„¤ (unitLattice K)).ofZlatticeBasis â„
  rsuffices âŸ¨âŸ¨_, âŸ¨u, _, rflâŸ©âŸ©, huâŸ© : âˆƒ e : unitLattice K, e + logMap x âˆˆ Zspan.fundamentalDomain B
  Â· exact âŸ¨u, by rwa [fundamentalCone, Set.mem_preimage, logMap_unitSMul u hx]âŸ©
  Â· obtain âŸ¨âŸ¨e, hâ‚âŸ©, hâ‚‚, -âŸ© := Zspan.exist_unique_vadd_mem_fundamentalDomain B (logMap x)
    exact âŸ¨âŸ¨e, by rwa [â† Basis.ofZlatticeBasis_span â„ (unitLattice K)]âŸ©, hâ‚‚âŸ©

theorem torsion_unitSMul_mem_of_mem {x : E K}
    (hx' : x âˆˆ fundamentalCone K) {Î¶ : (ğ“ K)Ë£} (hÎ¶ : Î¶ âˆˆ torsion K) :
    Î¶ â€¢ x âˆˆ fundamentalCone K := by
  rwa [fundamentalCone, Set.mem_preimage, logMap_torsion_unitSMul _ hÎ¶]

theorem unitSMul_mem_iff_mem_torsion {x : E K} (hx : mixedEmbedding.norm x â‰  0)
    (hx' : x âˆˆ fundamentalCone K) (u : (ğ“ K)Ë£) :
    u â€¢ x âˆˆ fundamentalCone K â†” u âˆˆ torsion K := by
  classical
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· rw [â† logEmbedding_eq_zero_iff]
    let B := (Module.Free.chooseBasis â„¤ (unitLattice K)).ofZlatticeBasis â„
    refine (Subtype.mk_eq_mk (h := ?_) (h' := ?_)).mp <|
      ExistsUnique.unique (Zspan.exist_unique_vadd_mem_fundamentalDomain B (logMap x)) ?_ ?_
    Â· change logEmbedding K u âˆˆ (Submodule.span â„¤ (Set.range B)).toAddSubgroup
      rw [Basis.ofZlatticeBasis_span â„ (unitLattice K)]
      exact âŸ¨u, trivial, rflâŸ©
    Â· exact Submodule.zero_mem _
    Â· rwa [fundamentalCone, Set.mem_preimage, logMap_unitSMul _ hx] at h
    Â· rw [AddSubmonoid.mk_vadd, vadd_eq_add, zero_add]
      rwa [fundamentalCone, Set.mem_preimage] at hx'
  Â· exact torsion_unitSMul_mem_of_mem hx' h

variable (K) in
/-- The set of images by `mixedEmbedding` of algebraic integers of `K` contained in the
fundamental cone. -/
def integralPoint : Set (E K) := (fundamentalCone K) âˆ© (mixedEmbedding K '' (ğ“ K))

theorem exists_unique_preimage_of_integralPoint {x : E K} (hx : x âˆˆ integralPoint K) :
    âˆƒ! a : (ğ“ K), mixedEmbedding K a = x := by
  refine âŸ¨âŸ¨hx.2.choose, hx.2.choose_spec.1âŸ©, hx.2.choose_spec.2, fun x h â†¦ ?_âŸ©
  rw [Subtype.ext_iff, â† (mixedEmbedding_injective K).eq_iff, h, hx.2.choose_spec.2]

/-- For `a : fundamentalCone K`, the unique algebraic integer which image by `mixedEmbedding` is
equal to `a`. -/
def preimageOfIntegralPoint (a : integralPoint K) : (ğ“ K) :=
  âŸ¨a.prop.2.choose, a.prop.2.choose_spec.1âŸ©

@[simp]
theorem image_preimageOfIntegralPoint (a : integralPoint K) :
    mixedEmbedding K (preimageOfIntegralPoint a) = a := a.prop.2.choose_spec.2

theorem preimageOfIntegralPoint_mixedEmbedding {x : ğ“ K}
    (hx : mixedEmbedding K x âˆˆ integralPoint K) :
    preimageOfIntegralPoint (âŸ¨mixedEmbedding K x, hxâŸ©) = x := by
  rw [Subtype.ext_iff, â† (mixedEmbedding_injective K).eq_iff, image_preimageOfIntegralPoint]

theorem exists_unitSMul_mem_integralPoint {x : E K} (hx : x âˆˆ mixedEmbedding K '' (ğ“ K)) :
    âˆƒ u : (ğ“ K)Ë£, u â€¢ x âˆˆ integralPoint K := by
  by_cases hx' : x = 0
  Â· simp_rw [hx', smul_zero]
    exact âŸ¨1, fundamentalCone.zero_mem _, âŸ¨0, zero_mem _, map_zero _âŸ©âŸ©
  Â· replace hx' : mixedEmbedding.norm x â‰  0 :=
      (norm_eq_zero_iff' (Set.mem_range_of_mem_image (mixedEmbedding K) _ hx)).not.mpr hx'
    obtain âŸ¨u, huâŸ© := exists_unitSMul_mem hx'
    obtain âŸ¨x, âŸ¨hxâ‚, âŸ¨_, rflâŸ©âŸ©âŸ© := hx
    refine âŸ¨u, hu, ?_âŸ©
    rw [unitSMul_smul, â† map_mul]
    exact âŸ¨u * x,  mul_mem (SetLike.coe_mem (u : ğ“ K)) hxâ‚, rflâŸ©

theorem torsion_unitSMul_mem_integralPoint {x : E K} {Î¶ : (ğ“ K)Ë£} (hÎ¶ : Î¶ âˆˆ torsion K)
    (hx : x âˆˆ integralPoint K) :
    Î¶ â€¢ x âˆˆ integralPoint K := by
  obtain âŸ¨a, ha, rflâŸ© := hx.2
  refine âŸ¨torsion_unitSMul_mem_of_mem hx.1 hÎ¶, âŸ¨Î¶ * a, ?_, ?_âŸ©âŸ©
  Â· exact mul_mem (SetLike.coe_mem (Î¶ : (ğ“ K))) ha
  Â· rw [unitSMul_smul, map_mul]

variable (K) in
/-- The map that sends an element of `a : fundamentalCone K` to the associates class
of its preimage in `(ğ“ K)`. By quotienting by the kernel of the map, which is equal to the group of
roots of unity, we get the equivalence `integralPointQuotEquivAssociates`. -/
def integralPointToAssociates (a : integralPoint K) : Associates (ğ“ K) :=
  âŸ¦preimageOfIntegralPoint aâŸ§

@[simp]
theorem integralPointToAssociates_apply (a : integralPoint K) :
    integralPointToAssociates K a = âŸ¦preimageOfIntegralPoint aâŸ§ := rfl

variable (K) in
theorem integralPointToAssociates_surjective :
    Function.Surjective (integralPointToAssociates K) := by
  rintro âŸ¨xâŸ©
  obtain âŸ¨u, huâŸ© : âˆƒ u : (ğ“ K)Ë£, u â€¢ (mixedEmbedding K x) âˆˆ integralPoint K :=
      exists_unitSMul_mem_integralPoint âŸ¨x, SetLike.coe_mem x, rflâŸ©
  refine âŸ¨âŸ¨u â€¢ (mixedEmbedding K x), huâŸ©, ?_âŸ©
  refine Quotient.sound âŸ¨uâ»Â¹, ?_âŸ©
  simp_rw [unitSMul_smul, â† map_mul, â† Submonoid.coe_mul]
  rw [preimageOfIntegralPoint_mixedEmbedding, mul_comm, Units.inv_mul_cancel_left]

@[simps]
instance integralPoint_torsionSMul: SMul (torsion K) (integralPoint K) where
  smul := fun âŸ¨Î¶, hÎ¶âŸ© âŸ¨x, hxâŸ© â†¦ âŸ¨Î¶ â€¢ x, torsion_unitSMul_mem_integralPoint hÎ¶ hxâŸ©

instance : MulAction (torsion K) (integralPoint K) where
  one_smul := fun _ â†¦ by
    rw [Subtype.mk_eq_mk, integralPoint_torsionSMul_smul_coe, OneMemClass.coe_one, one_smul]
  mul_smul := fun _ _ _ â†¦ by
    rw [Subtype.mk_eq_mk]
    simp_rw [integralPoint_torsionSMul_smul_coe, Submonoid.coe_mul, mul_smul]

theorem integralPoint_torsionSMul_stabilizer {a : integralPoint K} (ha : (a : E K) â‰  0) :
    MulAction.stabilizer (torsion K) a = âŠ¥ := by
  refine (Subgroup.eq_bot_iff_forall _).mpr fun Î¶ hÎ¶ â†¦ ?_
  rwa [MulAction.mem_stabilizer_iff, Subtype.ext_iff, integralPoint_torsionSMul_smul_coe,
    unitSMul_smul, â† image_preimageOfIntegralPoint, â† map_mul, (mixedEmbedding_injective K).eq_iff,
    mul_eq_rightâ‚€, OneMemClass.coe_eq_one, Units.val_eq_one, OneMemClass.coe_eq_one] at hÎ¶
  contrapose! ha
  rw [â† image_preimageOfIntegralPoint, ha, map_zero]

theorem integralPointToAssociates_eq_iff (a b : integralPoint K) :
    integralPointToAssociates K a = integralPointToAssociates K b â†”
      âˆƒ Î¶ : torsion K, Î¶ â€¢ a = b := by
  rw [integralPointToAssociates_apply, integralPointToAssociates_apply]
  rw [show âŸ¦preimageOfIntegralPoint aâŸ§ = âŸ¦preimageOfIntegralPoint bâŸ§ â†”
    âˆƒ u : (ğ“ K)Ë£, preimageOfIntegralPoint a * u = preimageOfIntegralPoint b by
    rw [Associates.mk_eq_mk_iff_associated, Associated]]
  simp_rw [mul_comm, â† unitSMul_eq_iff_mul_eq, image_preimageOfIntegralPoint, Subtype.ext_iff,
    integralPoint_torsionSMul_smul_coe]
  refine âŸ¨fun âŸ¨u, hâŸ© â†¦ ?_, fun âŸ¨âŸ¨Î¶, _âŸ©, hâŸ© â†¦ âŸ¨Î¶, hâŸ©âŸ©
  by_cases ha : (a : E K) = 0
  Â· simp_rw [ha, smul_zero] at h âŠ¢
    exact âŸ¨1, hâŸ©
  Â· have hnz : mixedEmbedding.norm (a : E K) â‰  0 :=
      (norm_eq_zero_iff' âŸ¨a.prop.2.choose, a.prop.2.choose_spec.2âŸ©).not.mpr ha
    refine âŸ¨âŸ¨u, (unitSMul_mem_iff_mem_torsion hnz a.prop.1 u).mp ?_âŸ©, hâŸ©
    rw [h]
    exact b.prop.1

variable (K) in
/-- The equivalence between `fundamentalCone.integralPoint K / torsion K` and `Associates K`. By
composing with the equivalence between `Associates K` and the principal ideals of `ğ“ K`, we get the
equivalence `integralPointQuotEquivIsPrincipal`. -/
def integralPointQuotEquivAssociates :
    Quotient (MulAction.orbitRel (torsion K) (integralPoint K)) â‰ƒ Associates (ğ“ K) := by
  refine Equiv.ofBijective (Quotient.lift (integralPointToAssociates K)
    fun _ _ h â†¦ ((integralPointToAssociates_eq_iff _ _).mpr h).symm)
    âŸ¨?_, (Quot.surjective_lift _).mpr (integralPointToAssociates_surjective K)âŸ©
  convert Setoid.ker_lift_injective (integralPointToAssociates K)
  all_goals
  Â· ext a b
    rw [Setoid.ker_def, eq_comm, integralPointToAssociates_eq_iff b a,
      MulAction.orbitRel_apply, MulAction.mem_orbit_iff]

@[simp]
theorem integralPointQuotEquivAssociates_apply (a : integralPoint K) :
    integralPointQuotEquivAssociates K âŸ¦aâŸ§ = âŸ¦preimageOfIntegralPoint aâŸ§ := rfl

variable (K) in
/-- The equivalence between `fundamentalCone.integralPoint K / torsion K` and the principal
ideals of `ğ“ K`. -/
def integralPointQuotEquivIsPrincipal :
    Quotient (MulAction.orbitRel (torsion K) (integralPoint K)) â‰ƒ
      {I : Ideal (ğ“ K) // Submodule.IsPrincipal I} :=
  (integralPointQuotEquivAssociates K).trans (Ideal.associatesEquivIsPrincipal (ğ“ K))

theorem integralPointQuotEquivIsPrincipal_apply (a : integralPoint K) :
    integralPointQuotEquivIsPrincipal K âŸ¦aâŸ§ = Ideal.span {preimageOfIntegralPoint a} := by
  rw [integralPointQuotEquivIsPrincipal, Equiv.trans_apply,
    integralPointQuotEquivAssociates_apply, Ideal.associatesEquivIsPrincipal_apply]

variable (K) in
/-- The norm `mixedEmbedding.norm` defined on `fundamentalCone.integralPoint K` lifts to a function
on the classes of `fundamentalCone.integralPoint K` modulo `torsion K`. -/
def integralPointQuotNorm :
    Quotient (MulAction.orbitRel (torsion K) (integralPoint K)) â†’ â„ := by
  refine Quotient.lift (fun x â†¦ mixedEmbedding.norm (x : E K)) fun a b âŸ¨u, huâŸ© â†¦ ?_
  simp_rw [â† hu, integralPoint_torsionSMul_smul_coe, norm_unitSMul]

theorem integralPointQuotNorm_apply (a : integralPoint K) :
    integralPointQuotNorm K âŸ¦aâŸ§ = mixedEmbedding.norm (a : E K) := rfl

theorem integralPointQuotNorm_eq_norm (a : integralPoint K) :
    integralPointQuotNorm K âŸ¦aâŸ§ = |Algebra.norm â„¤ (preimageOfIntegralPoint a)| := by
  rw [integralPointQuotNorm_apply, â† image_preimageOfIntegralPoint, norm_eq_norm,
    â† Algebra.coe_norm_int, Rat.cast_abs, Rat.cast_intCast, Int.cast_abs]

theorem integralPointQuotNorm_eq_zero_iff
    (q : Quotient (MulAction.orbitRel (torsion K) (integralPoint K))) :
    integralPointQuotNorm K q = 0 â†” Quotient.out' q = (0 : E K) := by
  convert_to integralPointQuotNorm K âŸ¦Quotient.out' qâŸ§ = 0 â†” Quotient.out' q = (0 : E K)
  Â· rw [â† @Quotient.mk''_eq_mk, Quotient.out_eq']
  Â· rw [integralPointQuotNorm_apply, norm_eq_zero_iff' (by
      rw [â† image_preimageOfIntegralPoint]
      exact Set.mem_range_self _)]

variable (K) in
/-- The equivalence between classes in `fundamentalCone.integralPoint K / torsion K` of norm `n`
and the principal ideals of `ğ“ K` of norm `n`. -/
def integralPointQuotNormEquivIsPrincipal (n : â„•) :
    { x // integralPointQuotNorm K x = n } â‰ƒ
        { I : Ideal (ğ“ K) // Submodule.IsPrincipal I âˆ§ Ideal.absNorm I = n } := by
  refine Equiv.trans ?_ (Equiv.subtypeSubtypeEquivSubtypeInter _ _)
  refine Equiv.subtypeEquiv (integralPointQuotEquivIsPrincipal K) ?_
  rintro âŸ¨aâŸ©
  rw [show Quot.mk _ a = âŸ¦aâŸ§ by rfl, integralPointQuotEquivIsPrincipal_apply,
    integralPointQuotNorm_eq_norm, Ideal.absNorm_span_singleton, Int.abs_eq_natAbs,
    Int.cast_natCast, Nat.cast_inj]

/-- For `n` positive, the number of principal ideals in `ğ“ K` of norm `n` multiplied by the number
of roots of unity in `K` is equal to the number of `fundamentalCone.integralPoint K` of
norm `n`. -/
theorem card_isPrincipal_norm_mul {n : â„•} (hn : 1 â‰¤ n) :
    Nat.card {I : Ideal (ğ“ K) // Submodule.IsPrincipal I âˆ§ Ideal.absNorm I = n} *
      Fintype.card (torsion K) =
        Nat.card ({a : integralPoint K // mixedEmbedding.norm (a : E K) = n}) := by
  rw [â† Nat.card_congr (integralPointQuotNormEquivIsPrincipal K n), â† Nat.card_eq_fintype_card,
    â† Nat.card_prod]
  refine Nat.card_congr (Equiv.symm ?_)
  refine (Equiv.subtypeEquiv (q := fun s â†¦ integralPointQuotNorm K s.fst = n)
    (MulAction.selfEquivSigmaOrbitsQuotientStabilizer (torsion K) (integralPoint K))
      fun _ â†¦ ?_).trans  ?_
  Â· simp only [MulAction.selfEquivSigmaOrbitsQuotientStabilizer,
      MulAction.selfEquivSigmaOrbitsQuotientStabilizer', MulAction.selfEquivSigmaOrbits',
      Quotient.mk'_eq_mk, Equiv.instTransSortSortSortEquivEquivEquiv_trans, Equiv.trans_apply,
      Equiv.sigmaCongrRight_apply, Equiv.sigmaFiberEquiv_symm_apply_fst, Equiv.Set.ofEq_apply,
      integralPointQuotNorm_apply]
  Â· refine (@Equiv.subtypeSigmaEquiv _ _ (fun q â†¦ integralPointQuotNorm K q = n)).trans
      (Equiv.sigmaEquivProdOfEquiv fun âŸ¨_, hâŸ© â†¦ ?_)
    rw [integralPoint_torsionSMul_stabilizer]
    exact QuotientGroup.quotientBot.toEquiv
    rw [ne_eq, â† integralPointQuotNorm_eq_zero_iff, h, Nat.cast_eq_zero, â† ne_eq]
    linarith

end fundamentalCone

end
