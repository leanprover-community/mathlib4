/-
Copyright (c) 2024 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.NumberTheory.NumberField.Units.DirichletTheorem

/-!
# Fundamental Cone

Let `K` be a number field of signature `(râ‚, râ‚‚)`. We define an action of the units `(ğ“ K)Ë£` on
the space `â„^râ‚ Ã— â„‚^râ‚‚` via the `mixedEmbedding`. The fundamental cone is a cone in `â„^râ‚ Ã— â„‚^râ‚‚`
that is a fundamental domain for the action of `(ğ“ K)Ë£` up to roots of unity.

## Main definitions and results

* `NumberField.mixedEmbedding.unitSMul`: the action of `(ğ“ K)Ë£` on `â„^râ‚ Ã— â„‚^râ‚‚` defined, for
`u : (ğ“ K)Ë£`, by multiplication component by component with `mixedEmbedding K u`.

* `NumberField.mixedEmbedding.fundamentalCone`: a cone in `â„^râ‚ Ã— â„‚^râ‚‚` --that is a subset stable
by multiplication by a real number, see `smul_mem_of_mem`--, that is also a fundamental domain
for the action of `(ğ“ K)Ë£` up to roots of unity, see `exists_unitSMul_me` and
`torsion_unitSMul_mem_of_mem`.

* `NumberField.mixedEmbedding.fundamentalCone.integralPoint`: the subset of elements of the
fundamental cone that are images of algebraic integers of `K`.

* `NumberField.mixedEmbedding.fundamentalCone.integralPointEquiv`: the equivalence between
`fundamentalCone.integralPoint K` and the principal non-zero ideals of `ğ“ K` times the
torsion of `K`.

* `NumberField.mixedEmbedding.fundamentalCone.card_isPrincipal_norm_eq`: the number of principal
non-zero ideals in `ğ“ K` of norm `n` multiplied by the number of roots of unity is
equal to the number of `fundamentalCone.integralPoint K` of norm `n`.

## Tags

number field, canonical embedding, principal ideals
-/

variable (K : Type*) [Field K]

namespace NumberField.mixedEmbedding

open NumberField NumberField.InfinitePlace

/-- The space `â„^râ‚ Ã— â„‚^râ‚‚` with `(râ‚, râ‚‚)` the signature of `K`. -/
local notation "E" K =>
  ({w : InfinitePlace K // IsReal w} â†’ â„) Ã— ({w : InfinitePlace K // IsComplex w} â†’ â„‚)

noncomputable section UnitSMul

/-- The action of `(ğ“ K)Ë£` on `â„^râ‚ Ã— â„‚^râ‚‚` defined, for `u : (ğ“ K)Ë£`, by multiplication component
by component with `mixedEmbedding K u`. -/
@[simps]
instance unitSMul : SMul (ğ“ K)Ë£ (E K) where
  smul := fun u x â†¦ (mixedEmbedding K u) * x

instance : MulAction (ğ“ K)Ë£ (E K) where
  one_smul := fun _ â†¦ by simp_rw [unitSMul_smul, Units.coe_one, map_one, one_mul]
  mul_smul := fun _ _ _ â†¦ by simp_rw [unitSMul_smul, Units.coe_mul, map_mul, mul_assoc]

instance : SMulZeroClass (ğ“ K)Ë£ (E K) where
  smul_zero := fun _ â†¦ by simp_rw [unitSMul_smul, mul_zero]

variable [NumberField K]

theorem unitSMul_eq_iff_mul_eq {x y : (ğ“ K)} {u : (ğ“ K)Ë£} :
    u â€¢ mixedEmbedding K x = mixedEmbedding K y â†” u * x = y := by
  rw [unitSMul_smul, â† map_mul, Function.Injective.eq_iff, â† RingOfIntegers.coe_eq_algebraMap,
    â† map_mul, â† RingOfIntegers.ext_iff]
  exact mixedEmbedding_injective K

theorem norm_unitSMul (u : (ğ“ K)Ë£) (x : E K) :
    mixedEmbedding.norm (u â€¢ x) = mixedEmbedding.norm x := by
  rw [unitSMul_smul, map_mul, norm_unit, one_mul]

theorem unitSMul_eq_zero (u : (ğ“ K)Ë£) (x : E K) :
    u â€¢ x = 0 â†” x = 0 := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· ext w
    Â· have := congr_fun (congr_arg Prod.fst h) w
      rw [unitSMul_smul, Prod.fst_mul, Pi.mul_apply, Prod.fst_zero, Pi.zero_apply, mul_eq_zero]
        at this
      refine this.resolve_left (by simp)
    Â· have := congr_fun (congr_arg Prod.snd h) w
      rw [unitSMul_smul, Prod.snd_mul, Pi.mul_apply, Prod.snd_zero, Pi.zero_apply, mul_eq_zero]
        at this
      refine this.resolve_left (by simp)
  Â· rw [h, smul_zero]

end UnitSMul

noncomputable section logMap

open NumberField.Units NumberField.Units.dirichletUnitTheorem FiniteDimensional

variable [NumberField K] {K}

open Classical in
/-- The map from `â„^râ‚ Ã— â„‚^râ‚‚` to `{w : InfinitePlace K // w â‰  wâ‚€} â†’ â„` (with `wâ‚€` a fixed place)
defined in such way that: 1) it factors the map `logEmbedding`, see `logMap_eq_logEmbedding`;
2) it is constant on the lines `{c â€¢ x | c âˆˆ â„}`, see `logMap_smul`. -/
def logMap (x : E K) : {w : InfinitePlace K // w â‰  wâ‚€} â†’ â„ := fun w â†¦
    if hw : IsReal w.val then
      Real.log â€–x.1 âŸ¨w.val, hwâŸ©â€– - Real.log (mixedEmbedding.norm x) * (finrank â„š K : â„)â»Â¹
    else
      2 * (Real.log â€–x.2 âŸ¨w.val, not_isReal_iff_isComplex.mp hwâŸ©â€– -
        Real.log (mixedEmbedding.norm x) * (finrank â„š K : â„)â»Â¹)

@[simp]
theorem logMap_zero : logMap (0 : E K) = 0 := by
  ext
  simp_rw [logMap, Prod.fst_zero, Prod.snd_zero, map_zero, Pi.zero_apply, norm_zero, Real.log_zero,
    zero_mul, sub_zero, mul_zero, dite_eq_ite, ite_self]

theorem logMap_mul {x y : E K} (hx : mixedEmbedding.norm x â‰  0) (hy : mixedEmbedding.norm y â‰  0) :
    logMap (x * y) = logMap x + logMap y := by
  ext w
  simp_rw [Pi.add_apply, logMap]
  split_ifs with hw
  Â· rw [Prod.fst_mul, Pi.mul_apply, norm_mul, map_mul, Real.log_mul, Real.log_mul hx hy, add_mul]
    Â· ring
    Â· exact norm_ne_zero_iff.mpr <| (mixedEmbedding.norm_ne_zero_iff.mp hx).1 âŸ¨_, hwâŸ©
    Â· exact norm_ne_zero_iff.mpr <| (mixedEmbedding.norm_ne_zero_iff.mp hy).1 âŸ¨_, hwâŸ©
  Â· replace hw := not_isReal_iff_isComplex.mp hw
    rw [Prod.snd_mul, Pi.mul_apply, norm_mul, map_mul, Real.log_mul, Real.log_mul hx hy, add_mul]
    Â· ring
    Â· exact norm_ne_zero_iff.mpr <| (mixedEmbedding.norm_ne_zero_iff.mp hx).2 âŸ¨_, hwâŸ©
    Â· exact norm_ne_zero_iff.mpr <| (mixedEmbedding.norm_ne_zero_iff.mp hy).2 âŸ¨_, hwâŸ©

theorem logMap_eq_logEmbedding (u : (ğ“ K)Ë£) :
    logMap (mixedEmbedding K u) = logEmbedding K u := by
  ext
  simp_rw [logMap, mixedEmbedding.norm_unit, Real.log_one, zero_mul, sub_zero, logEmbedding,
    AddMonoidHom.coe_mk, ZeroHom.coe_mk, mult, Nat.cast_ite, ite_mul, Nat.cast_one, one_mul,
    Nat.cast_ofNat, mixedEmbedding, RingHom.prod_apply, Pi.ringHom_apply, norm_embedding_eq,
    norm_embedding_of_isReal]
  rfl

theorem logMap_unitSMul (u : (ğ“ K)Ë£) {x : E K} (hx : mixedEmbedding.norm x â‰  0) :
    logMap (u â€¢ x) = logEmbedding K u + logMap x := by
  rw [unitSMul_smul, logMap_mul (by rw [norm_unit]; norm_num) hx, logMap_eq_logEmbedding]

theorem logMap_torsion_unitSMul (x : E K) {Î¶ : (ğ“ K)Ë£} (hÎ¶ : Î¶ âˆˆ torsion K) :
    logMap (Î¶ â€¢ x) = logMap x := by
  ext
  simp_rw [logMap, unitSMul_smul, Prod.fst_mul, Prod.snd_mul, Pi.mul_apply, norm_mul, map_mul,
    norm_eq_norm, mixedEmbedding, RingHom.prod_apply, Pi.ringHom_apply,
    show |(Algebra.norm â„š) (Î¶ : K)| = 1 by exact NumberField.isUnit_iff_norm.mp (Units.isUnit Î¶),
    Rat.cast_one, one_mul, norm_embedding_eq, norm_embedding_of_isReal, (mem_torsion K).mp hÎ¶,
    one_mul]

theorem logMap_smul {x : E K} (hx : mixedEmbedding.norm x â‰  0) {c : â„} (hc : c â‰  0) :
    logMap (c â€¢ x) = logMap x := by
  rw [show c â€¢ x = ((fun _ â†¦ c, fun _ â†¦ c) : (E K)) * x by rfl, logMap_mul _ hx, add_left_eq_self]
  ext
  have hr : (finrank â„š K : â„) â‰  0 :=  Nat.cast_ne_zero.mpr (ne_of_gt finrank_pos)
  simp_rw [logMap, Pi.zero_apply, norm_real, Real.log_pow, mul_comm, inv_mul_cancel_leftâ‚€ hr,
    Real.norm_eq_abs, Complex.norm_eq_abs,  Complex.abs_ofReal, sub_self, mul_zero, dite_eq_ite,
    ite_self]
  rw [norm_real]
  exact pow_ne_zero _ (abs_ne_zero.mpr hc)

end logMap

noncomputable section

open NumberField.Units NumberField.Units.dirichletUnitTheorem nonZeroDivisors

variable [NumberField K]

open Classical in
/-- The fundamental cone is a cone in `â„^râ‚ Ã— â„‚^râ‚‚` --that is a subset fixed by multiplication by
a scalar, see `smul_mem_of_mem`--, that is also a fundamental domain for the action of `(ğ“ K)Ë£` up
to roots of unity, see `exists_unitSMul_mem` and `torsion_unitSMul_mem_of_mem`. -/
def fundamentalCone : Set (E K) :=
  logMapâ»Â¹' (Zspan.fundamentalDomain
    ((Module.Free.chooseBasis â„¤ (unitLattice K)).ofZlatticeBasis â„ _)) \
      {x | mixedEmbedding.norm x = 0}

namespace fundamentalCone

variable {K}

theorem smul_mem_of_mem {x : E K} (hx : x âˆˆ fundamentalCone K) {c : â„} (hc : c â‰  0) :
    c â€¢ x âˆˆ fundamentalCone K := by
  refine âŸ¨?_, ?_âŸ©
  Â· rw [Set.mem_preimage, logMap_smul hx.2 hc]
    exact hx.1
  Â· rw [Set.mem_setOf_eq, mixedEmbedding.norm_smul, mul_eq_zero, not_or]
    exact âŸ¨pow_ne_zero _ (abs_ne_zero.mpr hc), hx.2âŸ©

theorem exists_unitSMul_mem {x : E K} (hx : mixedEmbedding.norm x â‰  0) :
    âˆƒ u : (ğ“ K)Ë£, u â€¢ x âˆˆ fundamentalCone K := by
  classical
  let B := (Module.Free.chooseBasis â„¤ (unitLattice K)).ofZlatticeBasis â„
  rsuffices âŸ¨âŸ¨_, âŸ¨u, _, rflâŸ©âŸ©, huâŸ© : âˆƒ e : unitLattice K, e + logMap x âˆˆ Zspan.fundamentalDomain B
  Â· exact âŸ¨u, by rwa [Set.mem_preimage, logMap_unitSMul u hx], by simp [hx]âŸ©
  Â· obtain âŸ¨âŸ¨e, hâ‚âŸ©, hâ‚‚, -âŸ© := Zspan.exist_unique_vadd_mem_fundamentalDomain B (logMap x)
    exact âŸ¨âŸ¨e, by rwa [â† Basis.ofZlatticeBasis_span â„ (unitLattice K)]âŸ©, hâ‚‚âŸ©

theorem torsion_unitSMul_mem_of_mem {x : E K} (hx : x âˆˆ fundamentalCone K) {Î¶ : (ğ“ K)Ë£}
    (hÎ¶ : Î¶ âˆˆ torsion K) :
    Î¶ â€¢ x âˆˆ fundamentalCone K := by
  refine âŸ¨?_, ?_âŸ©
  Â· rw [Set.mem_preimage, logMap_torsion_unitSMul _ hÎ¶]
    exact hx.1
  Â· simpa only [unitSMul_smul, Set.mem_setOf_eq, map_mul, norm_eq_norm, Rat.cast_abs, mul_eq_zero,
    abs_eq_zero, Rat.cast_eq_zero, Algebra.norm_eq_zero_iff, RingOfIntegers.coe_eq_zero_iff,
    Units.ne_zero, false_or] using hx.2

theorem unitSMul_mem_iff_mem_torsion {x : E K} (hx : x âˆˆ fundamentalCone K) (u : (ğ“ K)Ë£) :
    u â€¢ x âˆˆ fundamentalCone K â†” u âˆˆ torsion K := by
  classical
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· rw [â† logEmbedding_eq_zero_iff]
    let B := (Module.Free.chooseBasis â„¤ (unitLattice K)).ofZlatticeBasis â„
    refine (Subtype.mk_eq_mk (h := ?_) (h' := ?_)).mp <|
      ExistsUnique.unique (Zspan.exist_unique_vadd_mem_fundamentalDomain B (logMap x)) ?_ ?_
    Â· change logEmbedding K u âˆˆ (Submodule.span â„¤ (Set.range B)).toAddSubgroup
      rw [Basis.ofZlatticeBasis_span â„ (unitLattice K)]
      exact âŸ¨u, trivial, rflâŸ©
    Â· exact Submodule.zero_mem _
    Â· rw [AddSubmonoid.mk_vadd, vadd_eq_add, â† logMap_unitSMul _ hx.2]
      exact h.1
    Â· rw [AddSubmonoid.mk_vadd, vadd_eq_add, zero_add]
      exact hx.1
  Â· exact torsion_unitSMul_mem_of_mem hx h

variable (K) in
/-- The set of images by `mixedEmbedding` of algebraic integers of `K` contained in the
fundamental cone. -/
def integralPoint : Set (E K) :=
  fundamentalCone K âˆ© mixedEmbedding K '' (Set.range (algebraMap (ğ“ K) K))

theorem exists_unique_preimage_of_integralPoint {x : E K} (hx : x âˆˆ integralPoint K) :
    âˆƒ! a : (ğ“ K), mixedEmbedding K a = x := by
  refine âŸ¨hx.2.choose_spec.1.choose, ?_, fun _ h â†¦ ?_âŸ©
  Â· convert hx.2.choose_spec.2
    exact hx.2.choose_spec.1.choose_spec
  Â· rw [RingOfIntegers.ext_iff, â† (mixedEmbedding_injective K).eq_iff, h]
    convert hx.2.choose_spec.2.symm
    exact hx.2.choose_spec.1.choose_spec

theorem integralPoint_ne_zero (a : integralPoint K) :
    (a : E K) â‰  0 := by
  by_contra!
  exact a.prop.1.2 (this.symm â–¸ mixedEmbedding.norm.map_zero')

/-- For `a : fundamentalCone K`, the unique non-zero algebraic integer which image by
`mixedEmbedding` is equal to `a`. -/
def preimageOfIntegralPoint (a : integralPoint K) : (ğ“ K)â° :=
  âŸ¨a.prop.2.choose_spec.1.choose, by
    simp_rw [mem_nonZeroDivisors_iff_ne_zero, ne_eq, RingOfIntegers.ext_iff,
      a.prop.2.choose_spec.1.choose_spec, â† (mixedEmbedding_injective K).eq_iff, map_zero,
      a.prop.2.choose_spec.2, integralPoint_ne_zero a, not_false_eq_true]âŸ©

@[simp]
theorem mixedEmbedding_preimageOfIntegralPoint (a : integralPoint K) :
    mixedEmbedding K (preimageOfIntegralPoint a : ğ“ K) = (a : E K) := by
  rw [RingOfIntegers.coe_eq_algebraMap, â† a.prop.2.choose_spec.2, preimageOfIntegralPoint,
    a.prop.2.choose_spec.1.choose_spec]

theorem preimageOfIntegralPoint_mixedEmbedding {x : (ğ“ K)â°}
    (hx : mixedEmbedding K (x : ğ“ K) âˆˆ integralPoint K) :
    preimageOfIntegralPoint (âŸ¨mixedEmbedding K (x : ğ“ K), hxâŸ©) = x := by
  simp_rw [Subtype.ext_iff, RingOfIntegers.ext_iff, â† (mixedEmbedding_injective K).eq_iff,
    mixedEmbedding_preimageOfIntegralPoint]

theorem exists_unitSMul_mem_integralPoint {x : E K} (hx : x â‰  0)
    (hx' : x âˆˆ mixedEmbedding K '' (Set.range (algebraMap (ğ“ K) K))) :
    âˆƒ u : (ğ“ K)Ë£, u â€¢ x âˆˆ integralPoint K := by
  replace hx : mixedEmbedding.norm x â‰  0 :=
      (norm_eq_zero_iff' (Set.mem_range_of_mem_image (mixedEmbedding K) _ hx')).not.mpr hx
  obtain âŸ¨u, huâŸ© := exists_unitSMul_mem hx
  obtain âŸ¨_, âŸ¨âŸ¨x, rflâŸ©, âŸ¨_, rflâŸ©âŸ©âŸ© := hx'
  exact âŸ¨u, hu, (u * x : K), âŸ¨u * x, rflâŸ©, by simp_rw [unitSMul_smul, â† map_mul]âŸ©

theorem torsion_unitSMul_mem_integralPoint {x : E K} {Î¶ : (ğ“ K)Ë£} (hÎ¶ : Î¶ âˆˆ torsion K)
    (hx : x âˆˆ integralPoint K) :
    Î¶ â€¢ x âˆˆ integralPoint K := by
  obtain âŸ¨_, âŸ¨a, rflâŸ©, rflâŸ© := hx.2
  refine âŸ¨torsion_unitSMul_mem_of_mem hx.1 hÎ¶, âŸ¨Î¶ * a, ?_, ?_âŸ©âŸ©
  Â· exact âŸ¨Î¶ * a, rflâŸ©
  Â· rw [unitSMul_smul, map_mul]

variable (K) in
/-- The map that sends an element of `a : fundamentalCone K` to the associates class
of its preimage in `(ğ“ K)â°`. By quotienting by the kernel of the map, which is equal to the group
of roots of unity, we get the equivalence `integralPointQuotEquivAssociates`. -/
def integralPointToAssociates (a : integralPoint K) : Associates (ğ“ K)â° :=
  âŸ¦preimageOfIntegralPoint aâŸ§

@[simp]
theorem integralPointToAssociates_apply (a : integralPoint K) :
    integralPointToAssociates K a = âŸ¦preimageOfIntegralPoint aâŸ§ := rfl

variable (K) in
theorem integralPointToAssociates_surjective :
    Function.Surjective (integralPointToAssociates K) := by
  rintro âŸ¨xâŸ©
  obtain âŸ¨u, huâŸ© : âˆƒ u : (ğ“ K)Ë£, u â€¢ mixedEmbedding K (x : ğ“ K) âˆˆ integralPoint K := by
    refine exists_unitSMul_mem_integralPoint ?_  âŸ¨(x : ğ“ K), Set.mem_range_self _, rflâŸ©
    rw [map_ne_zero, RingOfIntegers.coe_ne_zero_iff]
    exact nonZeroDivisors.coe_ne_zero _
  refine âŸ¨âŸ¨u â€¢ mixedEmbedding K (x : ğ“ K), huâŸ©,
    Quotient.sound âŸ¨(nonZeroDivisorsUnitsEquiv (ğ“ K)).symm uâ»Â¹, ?_âŸ©âŸ©
  simp_rw [Subtype.ext_iff, RingOfIntegers.ext_iff, â† (mixedEmbedding_injective K).eq_iff,
    Submonoid.coe_mul, map_mul, mixedEmbedding_preimageOfIntegralPoint,
    nonZeroDivisorsUnitsEquiv_symm_apply, unitSMul_smul, â† map_mul, mul_comm,
    Units.mul_inv_cancel_right]

@[simps]
instance integralPoint_torsionSMul: SMul (torsion K) (integralPoint K) where
  smul := fun âŸ¨Î¶, hÎ¶âŸ© âŸ¨x, hxâŸ© â†¦ âŸ¨Î¶ â€¢ x, torsion_unitSMul_mem_integralPoint hÎ¶ hxâŸ©

instance : MulAction (torsion K) (integralPoint K) where
  one_smul := fun _ â†¦ by
    rw [Subtype.mk_eq_mk, integralPoint_torsionSMul_smul_coe, OneMemClass.coe_one, one_smul]
  mul_smul := fun _ _ _ â†¦ by
    rw [Subtype.mk_eq_mk]
    simp_rw [integralPoint_torsionSMul_smul_coe, Submonoid.coe_mul, mul_smul]

theorem integralPointToAssociates_eq_iff (a b : integralPoint K) :
    integralPointToAssociates K a = integralPointToAssociates K b â†”
      âˆƒ Î¶ : torsion K, Î¶ â€¢ a = b := by
  simp_rw [integralPointToAssociates_apply, Associates.quotient_mk_eq_mk,
    Associates.mk_eq_mk_iff_associated, Associated, mul_comm, Subtype.ext_iff,
    RingOfIntegers.ext_iff, â† (mixedEmbedding_injective K).eq_iff, Submonoid.coe_mul, map_mul,
    mixedEmbedding_preimageOfIntegralPoint, integralPoint_torsionSMul_smul_coe]
  refine âŸ¨fun âŸ¨u, huâŸ© â†¦ ?_, fun âŸ¨âŸ¨Î¶, _âŸ©, hâŸ© â†¦ ?_âŸ©
  Â· refine âŸ¨âŸ¨(nonZeroDivisorsUnitsEquiv (ğ“ K)) u, ?_âŸ©, by simp [hu]âŸ©
    exact (unitSMul_mem_iff_mem_torsion a.prop.1 _).mp (by simp [hu, b.prop.1])
  Â· exact âŸ¨(nonZeroDivisorsUnitsEquiv (ğ“ K)).symm Î¶, by rwa [nonZeroDivisorsUnitsEquiv_symm_apply]âŸ©

variable (K) in
/-- The equivalence between `fundamentalCone.integralPoint K / torsion K` and
`Associates (ğ“ K)â°`. -/
def integralPointQuotEquivAssociates :
    Quotient (MulAction.orbitRel (torsion K) (integralPoint K)) â‰ƒ Associates (ğ“ K)â° := by
  refine Equiv.ofBijective
    (Quotient.lift (integralPointToAssociates K)
      fun _ _ h â†¦ ((integralPointToAssociates_eq_iff _ _).mpr h).symm)
    âŸ¨by
      convert Setoid.ker_lift_injective (integralPointToAssociates K)
      all_goals
      Â· ext a b
        rw [Setoid.ker_def, eq_comm, integralPointToAssociates_eq_iff b a,
          MulAction.orbitRel_apply, MulAction.mem_orbit_iff],
      (Quot.surjective_lift _).mpr (integralPointToAssociates_surjective K)âŸ©

@[simp]
theorem integralPointQuotEquivAssociates_apply (a : integralPoint K) :
    integralPointQuotEquivAssociates K âŸ¦aâŸ§ = âŸ¦preimageOfIntegralPoint aâŸ§ := rfl

theorem integralPoint_torsionSMul_stabilizer {a : integralPoint K} :
    MulAction.stabilizer (torsion K) a = âŠ¥ := by
  refine (Subgroup.eq_bot_iff_forall _).mpr fun Î¶ hÎ¶ â†¦ ?_
  rwa [MulAction.mem_stabilizer_iff, Subtype.ext_iff, integralPoint_torsionSMul_smul_coe,
    unitSMul_smul, â† mixedEmbedding_preimageOfIntegralPoint, â† map_mul,
    (mixedEmbedding_injective K).eq_iff, â† map_mul, â† RingOfIntegers.ext_iff, mul_eq_rightâ‚€,
    Units.val_eq_one, OneMemClass.coe_eq_one] at hÎ¶
  exact nonZeroDivisors.coe_ne_zero _

open Submodule Ideal

variable (K) in
def integralPointEquiv :
    integralPoint K â‰ƒ {I : (Ideal (ğ“ K))â° // IsPrincipal I.val} Ã— torsion K :=
  (MulAction.selfEquivSigmaOrbitsQuotientStabilizer (torsion K) (integralPoint K)).trans
    ((Equiv.sigmaEquivProdOfEquiv (by
        intro _
        simp_rw [integralPoint_torsionSMul_stabilizer]
        exact QuotientGroup.quotientBot.toEquiv)).trans
      (Equiv.prodCongrLeft (fun _ â†¦ (integralPointQuotEquivAssociates K).trans
        (associatesNonZeroDivisorsEquivIsPrincipal (ğ“ K)))))

@[simp]
theorem integralPointEquiv_apply_fst (a : integralPoint K) :
    ((integralPointEquiv K a).1 : Ideal (ğ“ K)) = span {(preimageOfIntegralPoint a : ğ“ K)} := by
  simp_rw [â† associatesNonZeroDivisorsEquivIsPrincipal_apply,
    â† integralPointQuotEquivAssociates_apply]
  rfl

/-- The `mixedEmbedding.norm` of `a : integralPoint K` as a natural integer, see `intNorm_coe` . -/
def intNorm (a : integralPoint K) : â„• := (Algebra.norm â„¤ (preimageOfIntegralPoint a : ğ“ K)).natAbs

@[simp]
theorem intNorm_coe (a : integralPoint K) :
    (intNorm a : â„) = mixedEmbedding.norm (a : E K) := by
  rw [intNorm, Int.cast_natAbs, â† Rat.cast_intCast, Int.cast_abs, Algebra.coe_norm_int,
    â† norm_eq_norm, mixedEmbedding_preimageOfIntegralPoint]

/-- The norm `intNorm` defined on `fundamentalCone.integralPoint K` lifts to a function
on the classes of `fundamentalCone.integralPoint K` modulo `torsion K`. -/
def quotIntNorm :
    Quotient (MulAction.orbitRel (torsion K) (integralPoint K)) â†’ â„• :=
  Quotient.lift (fun x â†¦ intNorm x) fun a b âŸ¨u, huâŸ© â†¦ by
    rw [â† Nat.cast_inj (R := â„), intNorm_coe, intNorm_coe, â† hu, integralPoint_torsionSMul_smul_coe,
      norm_unitSMul]

@[simp]
theorem quotIntNorm_apply (a : integralPoint K) : quotIntNorm âŸ¦aâŸ§ = intNorm a := rfl

variable (K) in
def integralPointEquivNorm (n : â„•) :
    {a : integralPoint K // intNorm a = n} â‰ƒ
      {I : (Ideal (ğ“ K))â° // IsPrincipal (I : Ideal (ğ“ K)) âˆ§
        absNorm (I : Ideal (ğ“ K)) = n} Ã— (torsion K) :=
  calc {a // intNorm a = n}
      â‰ƒ {I : {I : (Ideal (ğ“ K))â° // IsPrincipal I.1} Ã— torsion K //
          absNorm (I.1 : Ideal (ğ“ K)) = n} :=
      (Equiv.subtypeEquiv (integralPointEquiv K) fun _ â†¦ by simp [intNorm, absNorm_span_singleton])
    _ â‰ƒ {I : {I : (Ideal (ğ“ K))â° // IsPrincipal I.1} // absNorm (I.1 : Ideal (ğ“ K)) = n} Ã—
          torsion K :=
      Equiv.prodSubtypeFstEquivSubtypeProd (p := fun I : {I : (Ideal (ğ“ K))â° // IsPrincipal I.1} â†¦
        absNorm (I : Ideal (ğ“ K)) = n)
    _ â‰ƒ {I : (Ideal (ğ“ K))â° // IsPrincipal (I : Ideal (ğ“ K)) âˆ§
          absNorm (I : Ideal (ğ“ K)) = n} Ã— (torsion K) :=
      Equiv.prodCongrLeft fun _ â†¦ (Equiv.subtypeSubtypeEquivSubtypeInter
        (fun I : (Ideal (ğ“ K))â° â†¦ IsPrincipal I.1) (fun I â†¦ absNorm I.1 = n))

@[simp]
theorem integralPointEquivNorm_apply_fst {n : â„•} {a : integralPoint K} (ha : intNorm a = n) :
    ((integralPointEquivNorm K n âŸ¨a, haâŸ©).1 : Ideal (ğ“ K)) =
      span {(preimageOfIntegralPoint a : ğ“ K)} := by
  simp_rw [â† associatesNonZeroDivisorsEquivIsPrincipal_apply,
    â† integralPointQuotEquivAssociates_apply]
  rfl

variable (K)

/-- For `n` positive, the number of `fundamentalCone.integralPoint K` of
norm `n` is equal to the number of principal ideals in `ğ“ K` of norm `n` multiplied by the number
of roots of unity in `K`. -/
theorem card_isPrincipal_norm_eq (n : â„•) :
    Nat.card {I : (Ideal (ğ“ K))â° | IsPrincipal (I : Ideal (ğ“ K)) âˆ§
      absNorm (I : Ideal (ğ“ K)) = n} * torsionOrder K =
        Nat.card {a : integralPoint K | intNorm a = n} := by
  rw [torsionOrder, PNat.mk_coe, â† Nat.card_eq_fintype_card, â† Nat.card_prod]
  exact Nat.card_congr (integralPointEquivNorm K n).symm

theorem card_isPrincipal_norm_le (n : â„•) :
    Nat.card {I : (Ideal (ğ“ K))â° | IsPrincipal (I : Ideal (ğ“ K)) âˆ§
      absNorm (I : Ideal (ğ“ K)) â‰¤ n} * torsionOrder K =
        Nat.card {a : integralPoint K | intNorm a â‰¤ n} := by
  rw [torsionOrder, PNat.mk_coe, â† Nat.card_eq_fintype_card, â† Nat.card_prod]
  refine Nat.card_congr <| @Equiv.ofFiberEquiv _ (Î³ := Finset.Iic n) _
      (fun I â†¦ âŸ¨absNorm (I.1 : Ideal (ğ“ K)), Finset.mem_Iic.mpr I.1.2.2âŸ©)
      (fun a â†¦ âŸ¨intNorm a.1, Finset.mem_Iic.mpr a.2âŸ©) fun âŸ¨i, hiâŸ© â†¦ ?_
  simp_rw [Subtype.mk.injEq]
  calc
    _ â‰ƒ {I : {I : (Ideal (ğ“ K))â° // IsPrincipal I.1 âˆ§ absNorm I.1 â‰¤ n} // absNorm I.1.1 = i}
          Ã— torsion K := Equiv.prodSubtypeFstEquivSubtypeProd
    _ â‰ƒ {I : (Ideal (ğ“ K))â° // (IsPrincipal I.1 âˆ§ absNorm I.1 â‰¤ n) âˆ§ absNorm I.1 = i}
          Ã— torsion K := Equiv.prodCongrLeft fun _ â†¦ (Equiv.subtypeSubtypeEquivSubtypeInter
      (p := fun I : (Ideal (ğ“ K))â° â†¦ IsPrincipal I.1 âˆ§ absNorm I.1 â‰¤ n)
      (q := fun I â†¦ absNorm I.1 = i))
    _ â‰ƒ {I : (Ideal (ğ“ K))â° // IsPrincipal I.1 âˆ§ absNorm I.1 = i âˆ§ absNorm I.1 â‰¤ n}
          Ã— torsion K := Equiv.prodCongrLeft fun _ â†¦ (Equiv.subtypeEquivRight fun _ â†¦ by aesop)
    _ â‰ƒ {I : (Ideal (ğ“ K))â° // IsPrincipal I.1 âˆ§ absNorm I.1 = i} Ã— torsion K :=
      Equiv.prodCongrLeft fun _ â†¦ (Equiv.subtypeEquivRight fun _ â†¦ by
      rw [and_iff_left_of_imp (a := absNorm _ = _) fun h â†¦ Finset.mem_Iic.mp (h â–¸ hi)])
    _ â‰ƒ {a : integralPoint K // intNorm a = i} := (integralPointEquivNorm K i).symm
    _ â‰ƒ {a : {a : integralPoint K // intNorm a â‰¤ n} // intNorm a.1 = i} :=
      (Equiv.subtypeSubtypeEquivSubtype fun h â†¦ Finset.mem_Iic.mp (h â–¸ hi)).symm

end fundamentalCone

end
