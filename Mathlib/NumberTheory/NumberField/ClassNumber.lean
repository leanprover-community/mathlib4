/-
Copyright (c) 2021 Anne Baanen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anne Baanen, Riccardo Brasca, Xavier Roblot
-/
import Mathlib.NumberTheory.ClassNumber.AdmissibleAbs
import Mathlib.NumberTheory.ClassNumber.Finite
import Mathlib.NumberTheory.NumberField.Discriminant.Basic
import Mathlib.RingTheory.Ideal.IsPrincipal
import Mathlib.NumberTheory.RamificationInertia.Galois

/-!
# Class numbers of number fields

This file defines the class number of a number field as the (finite) cardinality of
the class group of its ring of integers. It also proves some elementary results
on the class number.

## Main definitions
We denote by `M K` the Minkowski bound of a number field `K`, defined as
`(4 / Ï€) ^ nrComplexPlaces K * ((finrank â„š K)! / (finrank â„š K) ^ (finrank â„š K) * âˆš|discr K|)`.
- `NumberField.classNumber`: the class number of a number field is the (finite)
cardinality of the class group of its ring of integers
- `isPrincipalIdealRing_of_isPrincipal_of_pow_le_of_mem_primesOver_of_mem_Icc`: let `K`
  be a number field. To show that `ğ“ K` is a PID it is enough to show that, for all (natural) primes
  `p âˆˆ Finset.Icc 1 âŒŠ(M K)âŒ‹â‚Š`, all ideals `P` above `p` such that
  `p ^ (span ({p}).inertiaDeg P) â‰¤ âŒŠ(M K)âŒ‹â‚Š` are principal. This is the standard technique to prove
  that `ğ“ K` is principal, see [marcus1977number], discussion after Theorem 37.
  The way this theorem should be used is to first compute `âŒŠ(M K)âŒ‹â‚Š` and then to use `fin_cases`
  to deal with the finite number of primes `p` in the interval.
- `isPrincipalIdealRing_of_isPrincipal_of_lt_or_isPrincipal_of_mem_primesOver_of_mem_Icc`: let `K`
  be a number field such that `K/â„š` is Galois. To show that `ğ“ K` is a PID it is enough to show
  that, for all (natural) primes `p âˆˆ Finset.Icc 1 âŒŠ(M K)âŒ‹â‚Š`, there is an ideal `P` above `p` such
  that either `âŒŠ(M K)âŒ‹â‚Š < p ^ (span ({p}).inertiaDeg P)` or `P` is principal. This is the standard
  technique to prove that `ğ“ K` is principal in the Galois case, see [marcus1977number], discussion
  after Theorem 37.
  The way this theorem should be used is to first compute `âŒŠ(M K)âŒ‹â‚Š` and then to use `fin_cases`
  to deal with the finite number of primes `p` in the interval.
-/

open scoped nonZeroDivisors Real

open Module NumberField InfinitePlace Ideal Nat

variable (K : Type*) [Field K] [NumberField K]

local notation "M " K:70 => (4 / Ï€) ^ nrComplexPlaces K *
  ((finrank â„š K)! / (finrank â„š K) ^ (finrank â„š K) * âˆš|discr K|)

namespace NumberField

namespace RingOfIntegers

noncomputable instance instFintypeClassGroup : Fintype (ClassGroup (ğ“ K)) :=
  ClassGroup.fintypeOfAdmissibleOfFinite â„š K AbsoluteValue.absIsAdmissible

end RingOfIntegers

/-- The class number of a number field is the (finite) cardinality of the class group. -/
noncomputable def classNumber : â„• :=
  Fintype.card (ClassGroup (ğ“ K))

theorem classNumber_ne_zero : classNumber K â‰  0 := Fintype.card_ne_zero

theorem classNumber_pos : 0 < classNumber K := Fintype.card_pos

variable {K}

/-- The class number of a number field is `1` iff the ring of integers is a PID. -/
theorem classNumber_eq_one_iff : classNumber K = 1 â†” IsPrincipalIdealRing (ğ“ K) :=
  card_classGroup_eq_one_iff

theorem exists_ideal_in_class_of_norm_le (C : ClassGroup (ğ“ K)) :
    âˆƒ I : (Ideal (ğ“ K))â°, ClassGroup.mk0 I = C âˆ§
      absNorm (I : Ideal (ğ“ K)) â‰¤ M K := by
  obtain âŸ¨J, hJâŸ© := ClassGroup.mk0_surjective Câ»Â¹
  obtain âŸ¨_, âŸ¨a, ha, rflâŸ©, h_nz, h_nmâŸ© :=
    exists_ne_zero_mem_ideal_of_norm_le_mul_sqrt_discr K (FractionalIdeal.mk0 K J)
  obtain âŸ¨Iâ‚€, hIâŸ© := dvd_iff_le.mpr ((span_singleton_le_iff_mem J).mpr (by exact ha))
  have : Iâ‚€ â‰  0 := by
    contrapose! h_nz
    rw [h_nz, mul_zero, zero_eq_bot, span_singleton_eq_bot] at hI
    rw [Algebra.linearMap_apply, hI, map_zero]
  let I := (âŸ¨Iâ‚€, mem_nonZeroDivisors_iff_ne_zero.mpr thisâŸ© : (Ideal (ğ“ K))â°)
  refine âŸ¨I, ?_, ?_âŸ©
  Â· suffices ClassGroup.mk0 I = (ClassGroup.mk0 J)â»Â¹ by rw [this, hJ, inv_inv]
    exact ClassGroup.mk0_eq_mk0_inv_iff.mpr âŸ¨a, Subtype.coe_ne_coe.1 h_nz, by rw [mul_comm, hI]âŸ©
  Â· rw [â† FractionalIdeal.absNorm_span_singleton (ğ“ K), Algebra.linearMap_apply,
      â† FractionalIdeal.coeIdeal_span_singleton, FractionalIdeal.coeIdeal_absNorm, hI, map_mul,
      cast_mul, Rat.cast_mul, absNorm_apply, Rat.cast_natCast, Rat.cast_natCast,
      FractionalIdeal.coe_mk0, FractionalIdeal.coeIdeal_absNorm, Rat.cast_natCast, mul_div_assoc,
      mul_assoc, mul_assoc] at h_nm
    refine le_of_mul_le_mul_of_pos_left h_nm ?_
    exact cast_pos.mpr <| pos_of_ne_zero <| absNorm_ne_zero_of_nonZeroDivisors J

end NumberField

namespace RingOfIntegers

variable {K}

open scoped NumberField

theorem isPrincipalIdealRing_of_isPrincipal_of_norm_le
    (h : âˆ€ â¦ƒI : (Ideal (ğ“ K))â°â¦„, absNorm (I : Ideal (ğ“ K)) â‰¤ M K â†’
      Submodule.IsPrincipal (I : Ideal (ğ“ K))) : IsPrincipalIdealRing (ğ“ K) := by
  rw [â† classNumber_eq_one_iff, classNumber, Fintype.card_eq_one_iff]
  refine âŸ¨1, fun C â†¦ ?_âŸ©
  obtain âŸ¨I, rfl, hIâŸ© := exists_ideal_in_class_of_norm_le C
  simpa [â† ClassGroup.mk0_eq_one_iff] using h hI

theorem isPrincipalIdealRing_of_isPrincipal_of_norm_le_of_isPrime
    (h : âˆ€ â¦ƒI : (Ideal (ğ“ K))â°â¦„, (I : Ideal (ğ“ K)).IsPrime â†’
      absNorm (I : Ideal (ğ“ K)) â‰¤ M K â†’ Submodule.IsPrincipal (I : Ideal (ğ“ K))) :
    IsPrincipalIdealRing (ğ“ K) := by
  refine isPrincipalIdealRing_of_isPrincipal_of_norm_le (fun I hI â†¦ ?_)
  rw [â† mem_isPrincipalSubmonoid_iff,
    â† prod_normalizedFactors_eq_self (nonZeroDivisors.coe_ne_zero I)]
  refine Submonoid.multiset_prod_mem _ _ (fun J hJ â†¦ mem_isPrincipalSubmonoid_iff.mp ?_)
  by_cases hJ0 : J = 0
  Â· simpa [hJ0] using bot_isPrincipal
  rw [â† Subtype.coe_mk J (mem_nonZeroDivisors_of_ne_zero hJ0)]
  refine h (((mem_normalizedFactors_iff (nonZeroDivisors.coe_ne_zero I)).mp hJ).1) ?_
  exact (cast_le.mpr <| le_of_dvd (absNorm_pos_of_nonZeroDivisors I) <|
    absNorm_dvd_absNorm_of_le <| le_of_dvd <|
      UniqueFactorizationMonoid.dvd_of_mem_normalizedFactors hJ).trans hI

set_option linter.style.longLine false in
/-- Let `K` be a number field and let `M K` be the Minkowski bound of `K`.
To show that `ğ“ K` is a PID it is enough to show that, for all (natural) primes
`p âˆˆ Finset.Icc 1 âŒŠ(M K)âŒ‹â‚Š`, all ideals `P` above `p` such that
`p ^ (span ({p}).inertiaDeg P) â‰¤ âŒŠ(M K)âŒ‹â‚Š` are principal. This is the standard technique to prove
that `ğ“ K` is principal, see [marcus1977number], discussion after Theorem 37.
If `K/â„š` is Galois, one can use the more convenient
`RingOfIntegers.isPrincipalIdealRing_of_isPrincipal_of_lt_or_isPrincipal_of_mem_primesOver_of_mem_Icc`
below.

The way this theorem should be used is to first compute `âŒŠ(M K)âŒ‹â‚Š` and then to use `fin_cases`
to deal with the finite number of primes `p` in the interval. -/
theorem isPrincipalIdealRing_of_isPrincipal_of_pow_le_of_mem_primesOver_of_mem_Icc
    (h : âˆ€ p âˆˆ Finset.Icc 1 âŒŠ(M K)âŒ‹â‚Š, p.Prime â†’ âˆ€ (P : Ideal (ğ“ K)),
      P âˆˆ primesOver (span {(p : â„¤)}) (ğ“ K) â†’ p ^ ((span ({â†‘p} : Set â„¤)).inertiaDeg P) â‰¤ âŒŠ(M K)âŒ‹â‚Š â†’
      Submodule.IsPrincipal P) : IsPrincipalIdealRing (ğ“ K) := by
  refine isPrincipalIdealRing_of_isPrincipal_of_norm_le_of_isPrime <|
    fun âŸ¨P, HPâŸ© hP hPN â†¦ ?_
  obtain âŸ¨p, hpâŸ© := IsPrincipalIdealRing.principal <| under â„¤ P
  have hp0 : p â‰  0 := fun h â†¦ nonZeroDivisors.coe_ne_zero âŸ¨P, HPâŸ© <|
    eq_bot_of_comap_eq_bot (R := â„¤) <| by simpa only [hp, submodule_span_eq, span_singleton_eq_bot]
  have hpprime := (span_singleton_prime hp0).mp
  simp only [â† submodule_span_eq, â† hp] at hpprime
  have hlies : P.LiesOver (span {p}) := by
    rcases abs_choice p with h | h <;>
    simpa [h, span_singleton_neg p, â† submodule_span_eq, â† hp] using over_under P
  have hspan : span {â†‘p.natAbs} = span {p} := by
    rcases abs_choice p with h | h <;> simp [h]
  have hple : p.natAbs ^ (span {(p.natAbs : â„¤)}).inertiaDeg P â‰¤ âŒŠ(M K)âŒ‹â‚Š := by
    refine le_floor ?_
    simpa only [hspan, â† cast_pow, â† absNorm_eq_pow_inertiaDeg P (hpprime (hP.under _))] using hPN
  have hpabsprime := Int.prime_iff_natAbs_prime.mp (hpprime (hP.under _))
  refine h _ ?_ hpabsprime _ âŸ¨hP, ?_âŸ© hple
  Â· suffices 0 < (span {(p.natAbs : â„¤)}).inertiaDeg P by
      exact Finset.mem_Icc.mpr âŸ¨hpabsprime.one_le, le_trans (le_pow this) hpleâŸ©
    have := (isPrime_of_prime (prime_span_singleton_iff.mpr <|
      hpprime (hP.under _))).isMaximal <| by simp [((hpprime (hP.under _))).ne_zero]
    exact hspan â–¸ inertiaDeg_pos ..
  Â· exact hspan â–¸ hlies

/-- Let `K` be a number field such that `K/â„š` is Galois and let `M K` be the Minkowski bound of `K`.
To show that `ğ“ K` is a PID it is enough to show that, for all (natural) primes
`p âˆˆ Finset.Icc 1 âŒŠ(M K)âŒ‹â‚Š`, there is an ideal `P` above `p` such that
either `âŒŠ(M K)âŒ‹â‚Š < p ^ (span ({p}).inertiaDeg P)` or `P` is principal. This is the standard
technique to prove that `ğ“ K` is principal in the Galois case, see [marcus1977number], discussion
after Theorem 37.

The way this theorem should be used is to first compute `âŒŠ(M K)âŒ‹â‚Š` and then to use `fin_cases`
to deal with the finite number of primes `p` in the interval. -/
theorem isPrincipalIdealRing_of_isPrincipal_of_lt_or_isPrincipal_of_mem_primesOver_of_mem_Icc
    [IsGalois â„š K] (h : âˆ€ p âˆˆ Finset.Icc 1 âŒŠ(M K)âŒ‹â‚Š, p.Prime â†’
      âˆƒ P âˆˆ primesOver (span {(p : â„¤)}) (ğ“ K),
        âŒŠ(M K)âŒ‹â‚Š < p ^ ((span ({â†‘p} : Set â„¤)).inertiaDeg P) âˆ¨
          Submodule.IsPrincipal P) :
      IsPrincipalIdealRing (ğ“ K) := by
  refine isPrincipalIdealRing_of_isPrincipal_of_pow_le_of_mem_primesOver_of_mem_Icc
    (fun p hpmem hp P âŸ¨hP1, hP2âŸ© hple â†¦ ?_)
  obtain âŸ¨Q, âŸ¨hQ1, hQ2âŸ©, HâŸ© := h p hpmem hp
  have := (isPrime_of_prime (prime_span_singleton_iff.mpr (prime_iff_prime_int.mp hp))).isMaximal
    (by simp [hp.ne_zero])
  by_cases h : âŒŠ(M K)âŒ‹â‚Š < p ^ ((span ({â†‘p} : Set â„¤)).inertiaDeg P)
  Â· linarith
  rw [inertiaDeg_eq_of_isGalois _ Q P â„š K] at H
  obtain âŸ¨Ïƒ, rflâŸ© := exists_map_eq_of_isGalois (span ({â†‘p} : Set â„¤)) Q P â„š K
  exact (H.resolve_left h).map_ringHom Ïƒ

theorem isPrincipalIdealRing_of_abs_discr_lt
    (h : |discr K| < (2 * (Ï€ / 4) ^ nrComplexPlaces K *
      ((finrank â„š K) ^ (finrank â„š K) / (finrank â„š K)!)) ^ 2) :
    IsPrincipalIdealRing (ğ“ K) := by
  have : 0 < finrank â„š K := finrank_pos -- Lean needs to know this for `positivity` to succeed
  rw [â† Real.sqrt_lt (by positivity) (by positivity), mul_assoc, â† inv_mul_lt_iffâ‚€' (by positivity),
    mul_inv, â† inv_pow, inv_div, inv_div, mul_assoc, Int.cast_abs] at h
  refine isPrincipalIdealRing_of_isPrincipal_of_norm_le (fun I hI â†¦ ?_)
  rw [absNorm_eq_one_iff.mp <| le_antisymm (Nat.lt_succ_iff.mp (cast_lt.mp
    (lt_of_le_of_lt hI h))) <| one_le_iff_ne_zero.mpr (absNorm_ne_zero_of_nonZeroDivisors I)]
  exact top_isPrincipal

end RingOfIntegers

namespace Rat

open NumberField

theorem classNumber_eq : NumberField.classNumber â„š = 1 :=
  classNumber_eq_one_iff.mpr <| IsPrincipalIdealRing.of_surjective
    Rat.ringOfIntegersEquiv.symm Rat.ringOfIntegersEquiv.symm.surjective

end Rat
