/-
Copyright (c) 2021 Anne Baanen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anne Baanen
-/
import Mathlib.NumberTheory.ClassNumber.AdmissibleAbs
import Mathlib.NumberTheory.ClassNumber.Finite
import Mathlib.NumberTheory.NumberField.Discriminant.Basic
import Mathlib.RingTheory.Ideal.IsPrincipal

/-!
# Class numbers of number fields

This file defines the class number of a number field as the (finite) cardinality of
the class group of its ring of integers. It also proves some elementary results
on the class number.

## Main definitions
- `NumberField.classNumber`: the class number of a number field is the (finite)
cardinality of the class group of its ring of integers
-/

namespace NumberField

variable (K : Type*) [Field K] [NumberField K]

namespace RingOfIntegers

noncomputable instance instFintypeClassGroup : Fintype (ClassGroup (ğ“ K)) :=
  ClassGroup.fintypeOfAdmissibleOfFinite â„š K AbsoluteValue.absIsAdmissible

end RingOfIntegers

/-- The class number of a number field is the (finite) cardinality of the class group. -/
noncomputable def classNumber : â„• :=
  Fintype.card (ClassGroup (ğ“ K))

variable {K}

/-- The class number of a number field is `1` iff the ring of integers is a PID. -/
theorem classNumber_eq_one_iff : classNumber K = 1 â†” IsPrincipalIdealRing (ğ“ K) :=
  card_classGroup_eq_one_iff

open Module NumberField.InfinitePlace Ideal Nat

open scoped nonZeroDivisors Real

theorem exists_ideal_in_class_of_norm_le (C : ClassGroup (ğ“ K)) :
    âˆƒ I : (Ideal (ğ“ K))â°, ClassGroup.mk0 I = C âˆ§
      absNorm (I : Ideal (ğ“ K)) â‰¤ (4 / Ï€) ^ nrComplexPlaces K *
      ((finrank â„š K)! / (finrank â„š K) ^ (finrank â„š K) * âˆš|discr K|) := by
  obtain âŸ¨J, hJâŸ© := ClassGroup.mk0_surjective Câ»Â¹
  obtain âŸ¨_, âŸ¨a, ha, rflâŸ©, h_nz, h_nmâŸ© :=
    exists_ne_zero_mem_ideal_of_norm_le_mul_sqrt_discr K (FractionalIdeal.mk0 K J)
  obtain âŸ¨Iâ‚€, hIâŸ© := dvd_iff_le.mpr ((span_singleton_le_iff_mem J).mpr (by exact ha))
  have : Iâ‚€ â‰  0 := by
    contrapose! h_nz
    rw [h_nz, mul_zero, zero_eq_bot, span_singleton_eq_bot] at hI
    rw [Algebra.linearMap_apply, hI, map_zero]
  let I := (âŸ¨Iâ‚€, mem_nonZeroDivisors_iff_ne_zero.mpr thisâŸ© : (Ideal (ğ“ K))â°)
  refine âŸ¨I, ?_, ?_âŸ©
  Â· suffices ClassGroup.mk0 I = (ClassGroup.mk0 J)â»Â¹ by rw [this, hJ, inv_inv]
    exact ClassGroup.mk0_eq_mk0_inv_iff.mpr âŸ¨a, Subtype.coe_ne_coe.1 h_nz, by rw [mul_comm, hI]âŸ©
  Â· rw [â† FractionalIdeal.absNorm_span_singleton (ğ“ K), Algebra.linearMap_apply,
      â† FractionalIdeal.coeIdeal_span_singleton, FractionalIdeal.coeIdeal_absNorm, hI, map_mul,
      cast_mul, Rat.cast_mul, absNorm_apply, Rat.cast_natCast, Rat.cast_natCast,
      FractionalIdeal.coe_mk0, FractionalIdeal.coeIdeal_absNorm, Rat.cast_natCast, mul_div_assoc,
      mul_assoc, mul_assoc] at h_nm
    refine le_of_mul_le_mul_of_pos_left h_nm ?_
    exact cast_pos.mpr <| pos_of_ne_zero <| absNorm_ne_zero_of_nonZeroDivisors J

theorem _root_.RingOfIntegers.isPrincipalIdealRing_of_isPrincipal_of_norm_le
    (h : âˆ€ â¦ƒI : (Ideal (ğ“ K))â°â¦„, absNorm (I : Ideal (ğ“ K)) â‰¤ (4 / Ï€) ^ nrComplexPlaces K *
      ((finrank â„š K)! / (finrank â„š K) ^ (finrank â„š K) * âˆš|discr K|) â†’
      Submodule.IsPrincipal (I : Ideal (ğ“ K))) :
    IsPrincipalIdealRing (ğ“ K) := by
  rw [â† classNumber_eq_one_iff, classNumber, Fintype.card_eq_one_iff]
  refine âŸ¨1, fun C â†¦ ?_âŸ©
  obtain âŸ¨I, rfl, hIâŸ© := exists_ideal_in_class_of_norm_le C
  simpa [â† ClassGroup.mk0_eq_one_iff] using h hI

theorem _root_.RingOfIntegers.isPrincipalIdealRing_of_isPrincipal_of_norm_le_of_isPrime
    (h : âˆ€ â¦ƒI : (Ideal (ğ“ K))â°â¦„, (I : Ideal (ğ“ K)).IsPrime â†’
      absNorm (I : Ideal (ğ“ K)) â‰¤ (4 / Ï€) ^ nrComplexPlaces K *
      ((finrank â„š K)! / (finrank â„š K) ^ (finrank â„š K) * âˆš|discr K|) â†’
      Submodule.IsPrincipal (I : Ideal (ğ“ K))) :
    IsPrincipalIdealRing (ğ“ K) := by
  refine RingOfIntegers.isPrincipalIdealRing_of_isPrincipal_of_norm_le (fun I hI â†¦ ?_)
  rw [â† mem_isPrincipalSubmonoid_iff,
    â† prod_normalizedFactors_eq_self (nonZeroDivisors.coe_ne_zero I)]
  refine Submonoid.multiset_prod_mem _ _ (fun J hJ â†¦ mem_isPrincipalSubmonoid_iff.mp ?_)
  by_cases hJ0 : J = 0
  Â· simpa [hJ0] using bot_isPrincipal
  rw [â† Subtype.coe_mk J (mem_nonZeroDivisors_of_ne_zero hJ0)]
  refine h (((mem_normalizedFactors_iff (nonZeroDivisors.coe_ne_zero I)).mp hJ).1) ?_
  exact (cast_le.mpr <| le_of_dvd (absNorm_pos_of_nonZeroDivisors I) <|
    absNorm_dvd_absNorm_of_le <| le_of_dvd <|
      UniqueFactorizationMonoid.dvd_of_mem_normalizedFactors hJ).trans hI

theorem _root_.RingOfIntegers.isPrincipalIdealRing_of_isPrincipal_of_mem_primesOver
    (h : âˆ€ â¦ƒp : â„•â¦„, p.Prime â†’ p â‰¤ (4 / Ï€) ^ nrComplexPlaces K *
      ((finrank â„š K)! / (finrank â„š K) ^ (finrank â„š K) * âˆš|discr K|) â†’
      âˆ€ (I : Ideal (ğ“ K)), I âˆˆ Ideal.primesOver (span {(p : â„¤)}) (ğ“ K) â†’
      Submodule.IsPrincipal I) :
    IsPrincipalIdealRing (ğ“ K) := by
  refine RingOfIntegers.isPrincipalIdealRing_of_isPrincipal_of_norm_le_of_isPrime <|
    fun P hP hPNorm â†¦ ?_
  obtain âŸ¨p, hpâŸ© := IsPrincipalIdealRing.principal <| under â„¤ P.1
  have hp0 : p â‰  0 := fun h â†¦ nonZeroDivisors.coe_ne_zero P <|
    eq_bot_of_comap_eq_bot (R := â„¤) <| by simpa only [hp, submodule_span_eq, span_singleton_eq_bot]
  have hpprime := (span_singleton_prime hp0).mp
  rw [â† submodule_span_eq, â† hp] at hpprime
  have hle : algebraMap â„¤ (ğ“ K) p âˆˆ P.1 := (mem_of_liesOver P.1 (under â„¤ P.1) p).mp <|
    hp â–¸ Submodule.mem_span_singleton_self p
  refine h (Int.prime_iff_natAbs_prime.mp (hpprime (hP.under _))) ?_ _ âŸ¨hP, ?_âŸ©
  Â· refine le_trans (cast_le.mpr <| Nat.le_of_dvd ?_ (Int.ofNat_dvd_right.mp ?_)) hPNorm
    Â· exact Nat.pos_of_ne_zero <| fun h â†¦ nonZeroDivisors.coe_ne_zero P <| absNorm_eq_zero_iff.mp h
    suffices (Algebra.norm â„¤ (algebraMap â„¤ (ğ“ K) p)) = p ^ (Module.finrank â„š K) by
      obtain âŸ¨i, -, hiâŸ© := (dvd_prime_pow (hpprime (IsPrime.comap _)) _).mp
        (this â–¸ absNorm_dvd_norm_of_mem hle)
      refine dvd_trans (dvd_pow_self p (fun h â†¦ hP.ne_top (absNorm_eq_one_iff.mp ?_))) hi.dvd'
      simp only [h, pow_zero, associated_one_iff_isUnit] at hi
      exact ZMod.eq_one_of_isUnit_natCast hi
    exact Rat.intCast_injective (by simp [Algebra.coe_norm_int, â† Algebra.norm_algebraMap])
  Â· rcases abs_choice p with h | h <;>
    simpa [h, span_singleton_neg p, â† submodule_span_eq, â† hp] using over_under P.1


theorem _root_.RingOfIntegers.isPrincipalIdealRing_of_abs_discr_lt
    (h : |discr K| < (2 * (Ï€ / 4) ^ nrComplexPlaces K *
      ((finrank â„š K) ^ (finrank â„š K) / (finrank â„š K)!)) ^ 2) :
    IsPrincipalIdealRing (ğ“ K) := by
  have : 0 < finrank â„š K := finrank_pos -- Lean needs to know this for `positivity` to succeed
  rw [â† Real.sqrt_lt (by positivity) (by positivity), mul_assoc, â† inv_mul_lt_iffâ‚€' (by positivity),
    mul_inv, â† inv_pow, inv_div, inv_div, mul_assoc, Int.cast_abs] at h
  refine RingOfIntegers.isPrincipalIdealRing_of_isPrincipal_of_norm_le (fun I hI â†¦ ?_)
  convert top_isPrincipal
  exact absNorm_eq_one_iff.mp <| le_antisymm (lt_succ.mp (cast_lt.mp
    (lt_of_le_of_lt hI h))) <| one_le_iff_ne_zero.mpr (absNorm_ne_zero_of_nonZeroDivisors I)

end NumberField

namespace Rat

open NumberField

theorem classNumber_eq : NumberField.classNumber â„š = 1 :=
  classNumber_eq_one_iff.mpr <| IsPrincipalIdealRing.of_surjective
    Rat.ringOfIntegersEquiv.symm Rat.ringOfIntegersEquiv.symm.surjective

end Rat
