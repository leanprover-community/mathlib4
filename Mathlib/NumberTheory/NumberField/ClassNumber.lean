/-
Copyright (c) 2021 Anne Baanen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anne Baanen
-/
import Mathlib.NumberTheory.ClassNumber.AdmissibleAbs
import Mathlib.NumberTheory.ClassNumber.Finite
import Mathlib.NumberTheory.NumberField.Discriminant

#align_import number_theory.number_field.class_number from "leanprover-community/mathlib"@"d0259b01c82eed3f50390a60404c63faf9e60b1f"

/-!
# Class numbers of number fields

This file defines the class number of a number field as the (finite) cardinality of
the class group of its ring of integers. It also proves some elementary results
on the class number.

## Main definitions
- `NumberField.classNumber`: the class number of a number field is the (finite)
cardinality of the class group of its ring of integers
-/


namespace NumberField

variable (K : Type*) [Field K] [NumberField K]

namespace RingOfIntegers

noncomputable instance instFintypeClassGroup : Fintype (ClassGroup (ğ“ K)) :=
  ClassGroup.fintypeOfAdmissibleOfFinite â„š K AbsoluteValue.absIsAdmissible

end RingOfIntegers

/-- The class number of a number field is the (finite) cardinality of the class group. -/
noncomputable def classNumber : â„• :=
  Fintype.card (ClassGroup (ğ“ K))
#align number_field.class_number NumberField.classNumber

variable {K}

/-- The class number of a number field is `1` iff the ring of integers is a PID. -/
theorem classNumber_eq_one_iff : classNumber K = 1 â†” IsPrincipalIdealRing (ğ“ K) :=
  card_classGroup_eq_one_iff
#align number_field.class_number_eq_one_iff NumberField.classNumber_eq_one_iff

open FiniteDimensional NumberField.InfinitePlace

open scoped nonZeroDivisors Real

theorem exists_ideal_in_class_of_norm_le (C : ClassGroup (ğ“ K)) :
    âˆƒ I : (Ideal (ğ“ K))â°, ClassGroup.mk0 I = C âˆ§
      Ideal.absNorm (I : Ideal (ğ“ K)) â‰¤ (4 / Ï€) ^ NrComplexPlaces K *
        ((finrank â„š K).factorial / (finrank â„š K) ^ (finrank â„š K) * Real.sqrt |discr K|) := by
  obtain âŸ¨J, hJâŸ© := ClassGroup.mk0_surjective Câ»Â¹
  obtain âŸ¨_, âŸ¨a, ha, rflâŸ©, h_nz, h_nmâŸ© :=
    exists_ne_zero_mem_ideal_of_norm_le_mul_sqrt_discr K (FractionalIdeal.mk0 K J)
  obtain âŸ¨Iâ‚€, hIâŸ© := Ideal.dvd_iff_le.mpr ((Ideal.span_singleton_le_iff_mem J).mpr (by convert ha))
  have : Iâ‚€ â‰  0 := by
    contrapose! h_nz
    rw [h_nz, mul_zero, show 0 = (âŠ¥ : Ideal (ğ“ K)) by rfl, Ideal.span_singleton_eq_bot] at hI
    rw [Algebra.linearMap_apply, hI, map_zero]
  let I := (âŸ¨Iâ‚€, mem_nonZeroDivisors_iff_ne_zero.mpr thisâŸ© : (Ideal (ğ“ K))â°)
  refine âŸ¨I, ?_, ?_âŸ©
  Â· suffices ClassGroup.mk0 I = (ClassGroup.mk0 J)â»Â¹ by rw [this, hJ, inv_inv]
    exact ClassGroup.mk0_eq_mk0_inv_iff.mpr âŸ¨a, Subtype.coe_ne_coe.1 h_nz, by rw [mul_comm, hI]âŸ©
  Â· rw [â† FractionalIdeal.absNorm_span_singleton (ğ“ K), Algebra.linearMap_apply,
      â† FractionalIdeal.coeIdeal_span_singleton, FractionalIdeal.coeIdeal_absNorm, hI, map_mul,
      Nat.cast_mul, Rat.cast_mul, show Ideal.absNorm Iâ‚€ = Ideal.absNorm (I : Ideal (ğ“ K)) by rfl,
      Rat.cast_natCast, Rat.cast_natCast, FractionalIdeal.coe_mk0,
      FractionalIdeal.coeIdeal_absNorm, Rat.cast_natCast, mul_div_assoc, mul_assoc, mul_assoc]
      at h_nm
    refine le_of_mul_le_mul_of_pos_left h_nm ?_
    exact Nat.cast_pos.mpr <| Nat.pos_of_ne_zero <| Ideal.absNorm_ne_zero_of_nonZeroDivisors J

theorem _root_.RingOfIntegers.isPrincipalIdealRing_of_abs_discr_lt
    (h : |discr K| < (2 * (Ï€ / 4) ^ NrComplexPlaces K *
      ((finrank â„š K) ^ (finrank â„š K) / (finrank â„š K).factorial)) ^ 2) :
    IsPrincipalIdealRing (ğ“ K) := by
  have : 0 < finrank â„š K := finrank_pos -- Lean needs to know that for positivity to succeed
  rw [â† Real.sqrt_lt (by positivity) (by positivity), mul_assoc, â† inv_mul_lt_iff' (by positivity),
    mul_inv, â† inv_pow, inv_div, inv_div, mul_assoc, Int.cast_abs] at h
  rw [â† classNumber_eq_one_iff, classNumber, Fintype.card_eq_one_iff]
  refine âŸ¨1, fun C â†¦ ?_âŸ©
  obtain âŸ¨I, rfl, hIâŸ© := exists_ideal_in_class_of_norm_le C
  have : Ideal.absNorm I.1 = 1 := by
    refine le_antisymm (Nat.lt_succ.mp ?_) (Nat.one_le_iff_ne_zero.mpr
      (Ideal.absNorm_ne_zero_of_nonZeroDivisors I))
    exact Nat.cast_lt.mp <| lt_of_le_of_lt hI h
  rw [ClassGroup.mk0_eq_one_iff, Ideal.absNorm_eq_one_iff.mp this]
  exact top_isPrincipal

end NumberField

namespace Rat

open NumberField

theorem classNumber_eq : NumberField.classNumber â„š = 1 :=
  classNumber_eq_one_iff.mpr <| by
    convert IsPrincipalIdealRing.of_surjective
      (Rat.ringOfIntegersEquiv.symm: â„¤ â†’+* ğ“ â„š) Rat.ringOfIntegersEquiv.symm.surjective
#align rat.class_number_eq Rat.classNumber_eq

end Rat
