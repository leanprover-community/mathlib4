/-
Copyright (c) 2021 Anne Baanen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anne Baanen
-/
import Mathlib.NumberTheory.ClassNumber.AdmissibleAbs
import Mathlib.NumberTheory.ClassNumber.Finite
import Mathlib.NumberTheory.NumberField.Discriminant.Basic
import Mathlib.RingTheory.Ideal.IsPrincipal

/-!
# Class numbers of number fields

This file defines the class number of a number field as the (finite) cardinality of
the class group of its ring of integers. It also proves some elementary results
on the class number.

## Main definitions
- `NumberField.classNumber`: the class number of a number field is the (finite)
cardinality of the class group of its ring of integers
- `RingOfIntegers.isPrincipalIdealRing_of_isPrincipal_of_mem_primesOver_of_le`: to show that the
ring of integer of a number field is a PID it is enough to show that all ideals above any (natural)
prime `p` smaller than Minkowski bound are principal. This is the standard technique to prove that
`ùìû K` is principal, see [marcus1977number], discussion after Theorem 37.
-/

namespace NumberField

variable (K : Type*) [Field K] [NumberField K]

namespace RingOfIntegers

noncomputable instance instFintypeClassGroup : Fintype (ClassGroup (ùìû K)) :=
  ClassGroup.fintypeOfAdmissibleOfFinite ‚Ñö K AbsoluteValue.absIsAdmissible

end RingOfIntegers

/-- The class number of a number field is the (finite) cardinality of the class group. -/
noncomputable def classNumber : ‚Ñï :=
  Fintype.card (ClassGroup (ùìû K))

theorem classNumber_ne_zero : classNumber K ‚â† 0 := Fintype.card_ne_zero

theorem classNumber_pos : 0 < classNumber K := Fintype.card_pos

variable {K}

/-- The class number of a number field is `1` iff the ring of integers is a PID. -/
theorem classNumber_eq_one_iff : classNumber K = 1 ‚Üî IsPrincipalIdealRing (ùìû K) :=
  card_classGroup_eq_one_iff

open Module NumberField.InfinitePlace Ideal Nat

open scoped nonZeroDivisors Real

theorem exists_ideal_in_class_of_norm_le (C : ClassGroup (ùìû K)) :
    ‚àÉ I : (Ideal (ùìû K))‚Å∞, ClassGroup.mk0 I = C ‚àß
      absNorm (I : Ideal (ùìû K)) ‚â§ (4 / œÄ) ^ nrComplexPlaces K *
      ((finrank ‚Ñö K)! / (finrank ‚Ñö K) ^ (finrank ‚Ñö K) * ‚àö|discr K|) := by
  obtain ‚ü®J, hJ‚ü© := ClassGroup.mk0_surjective C‚Åª¬π
  obtain ‚ü®_, ‚ü®a, ha, rfl‚ü©, h_nz, h_nm‚ü© :=
    exists_ne_zero_mem_ideal_of_norm_le_mul_sqrt_discr K (FractionalIdeal.mk0 K J)
  obtain ‚ü®I‚ÇÄ, hI‚ü© := dvd_iff_le.mpr ((span_singleton_le_iff_mem J).mpr (by exact ha))
  have : I‚ÇÄ ‚â† 0 := by
    contrapose! h_nz
    rw [h_nz, mul_zero, zero_eq_bot, span_singleton_eq_bot] at hI
    rw [Algebra.linearMap_apply, hI, map_zero]
  let I := (‚ü®I‚ÇÄ, mem_nonZeroDivisors_iff_ne_zero.mpr this‚ü© : (Ideal (ùìû K))‚Å∞)
  refine ‚ü®I, ?_, ?_‚ü©
  ¬∑ suffices ClassGroup.mk0 I = (ClassGroup.mk0 J)‚Åª¬π by rw [this, hJ, inv_inv]
    exact ClassGroup.mk0_eq_mk0_inv_iff.mpr ‚ü®a, Subtype.coe_ne_coe.1 h_nz, by rw [mul_comm, hI]‚ü©
  ¬∑ rw [‚Üê FractionalIdeal.absNorm_span_singleton (ùìû K), Algebra.linearMap_apply,
      ‚Üê FractionalIdeal.coeIdeal_span_singleton, FractionalIdeal.coeIdeal_absNorm, hI, map_mul,
      cast_mul, Rat.cast_mul, absNorm_apply, Rat.cast_natCast, Rat.cast_natCast,
      FractionalIdeal.coe_mk0, FractionalIdeal.coeIdeal_absNorm, Rat.cast_natCast, mul_div_assoc,
      mul_assoc, mul_assoc] at h_nm
    refine le_of_mul_le_mul_of_pos_left h_nm ?_
    exact cast_pos.mpr <| pos_of_ne_zero <| absNorm_ne_zero_of_nonZeroDivisors J

theorem _root_.RingOfIntegers.isPrincipalIdealRing_of_isPrincipal_of_norm_le
    (h : ‚àÄ ‚¶ÉI : (Ideal (ùìû K))‚Å∞‚¶Ñ, absNorm (I : Ideal (ùìû K)) ‚â§ (4 / œÄ) ^ nrComplexPlaces K *
      ((finrank ‚Ñö K)! / (finrank ‚Ñö K) ^ (finrank ‚Ñö K) * ‚àö|discr K|) ‚Üí
      Submodule.IsPrincipal (I : Ideal (ùìû K))) :
    IsPrincipalIdealRing (ùìû K) := by
  rw [‚Üê classNumber_eq_one_iff, classNumber, Fintype.card_eq_one_iff]
  refine ‚ü®1, fun C ‚Ü¶ ?_‚ü©
  obtain ‚ü®I, rfl, hI‚ü© := exists_ideal_in_class_of_norm_le C
  simpa [‚Üê ClassGroup.mk0_eq_one_iff] using h hI

theorem _root_.RingOfIntegers.isPrincipalIdealRing_of_isPrincipal_of_norm_le_of_isPrime
    (h : ‚àÄ ‚¶ÉI : (Ideal (ùìû K))‚Å∞‚¶Ñ, (I : Ideal (ùìû K)).IsPrime ‚Üí
      absNorm (I : Ideal (ùìû K)) ‚â§ (4 / œÄ) ^ nrComplexPlaces K *
      ((finrank ‚Ñö K)! / (finrank ‚Ñö K) ^ (finrank ‚Ñö K) * ‚àö|discr K|) ‚Üí
      Submodule.IsPrincipal (I : Ideal (ùìû K))) :
    IsPrincipalIdealRing (ùìû K) := by
  refine RingOfIntegers.isPrincipalIdealRing_of_isPrincipal_of_norm_le (fun I hI ‚Ü¶ ?_)
  rw [‚Üê mem_isPrincipalSubmonoid_iff,
    ‚Üê prod_normalizedFactors_eq_self (nonZeroDivisors.coe_ne_zero I)]
  refine Submonoid.multiset_prod_mem _ _ (fun J hJ ‚Ü¶ mem_isPrincipalSubmonoid_iff.mp ?_)
  by_cases hJ0 : J = 0
  ¬∑ simpa [hJ0] using bot_isPrincipal
  rw [‚Üê Subtype.coe_mk J (mem_nonZeroDivisors_of_ne_zero hJ0)]
  refine h (((mem_normalizedFactors_iff (nonZeroDivisors.coe_ne_zero I)).mp hJ).1) ?_
  exact (cast_le.mpr <| le_of_dvd (absNorm_pos_of_nonZeroDivisors I) <|
    absNorm_dvd_absNorm_of_le <| le_of_dvd <|
      UniqueFactorizationMonoid.dvd_of_mem_normalizedFactors hJ).trans hI

/-- To show that the ring of integer of a number field is a PID it is enough to show that all ideals
above any (natural) prime `p`  smaller than Minkowski bound are principal. -/
theorem _root_.RingOfIntegers.isPrincipalIdealRing_of_isPrincipal_of_mem_primesOver_of_le
    (h : ‚àÄ ‚¶Ép : ‚Ñï‚¶Ñ, p.Prime ‚Üí p ‚â§ (4 / œÄ) ^ nrComplexPlaces K *
      ((finrank ‚Ñö K)! / (finrank ‚Ñö K) ^ (finrank ‚Ñö K) * ‚àö|discr K|) ‚Üí
      ‚àÄ (I : Ideal (ùìû K)), I ‚àà Ideal.primesOver (span {(p : ‚Ñ§)}) (ùìû K) ‚Üí
      Submodule.IsPrincipal I) :
    IsPrincipalIdealRing (ùìû K) := by
  refine RingOfIntegers.isPrincipalIdealRing_of_isPrincipal_of_norm_le_of_isPrime <|
    fun ‚ü®P, HP‚ü© hP hPNorm ‚Ü¶ ?_
  obtain ‚ü®p, hp‚ü© := IsPrincipalIdealRing.principal <| under ‚Ñ§ P
  have hp0 : p ‚â† 0 := fun h ‚Ü¶ nonZeroDivisors.coe_ne_zero ‚ü®P, HP‚ü© <|
    eq_bot_of_comap_eq_bot (R := ‚Ñ§) <| by simpa only [hp, submodule_span_eq, span_singleton_eq_bot]
  have hpprime := (span_singleton_prime hp0).mp
  rw [‚Üê submodule_span_eq, ‚Üê hp] at hpprime
  have hle : algebraMap ‚Ñ§ (ùìû K) p ‚àà P := (mem_of_liesOver P (under ‚Ñ§ P) p).mp <|
    hp ‚ñ∏ Submodule.mem_span_singleton_self p
  refine h (Int.prime_iff_natAbs_prime.mp (hpprime (hP.under _))) ?_ _ ‚ü®hP, ?_‚ü©
  ¬∑ refine le_trans (cast_le.mpr <| Nat.le_of_dvd ?_ (Int.ofNat_dvd_right.mp ?_)) hPNorm
    ¬∑ exact Nat.pos_of_ne_zero <| fun h ‚Ü¶ nonZeroDivisors.coe_ne_zero ‚ü®P, HP‚ü© <|
        absNorm_eq_zero_iff.mp h
    suffices (Algebra.norm ‚Ñ§ (algebraMap ‚Ñ§ (ùìû K) p)) = p ^ (Module.finrank ‚Ñö K) by
      obtain ‚ü®i, -, hi‚ü© := (dvd_prime_pow (hpprime (IsPrime.comap _)) _).mp
        (this ‚ñ∏ absNorm_dvd_norm_of_mem hle)
      refine dvd_trans (dvd_pow_self p (fun h ‚Ü¶ hP.ne_top (absNorm_eq_one_iff.mp ?_))) hi.dvd'
      simp only [h, pow_zero, associated_one_iff_isUnit] at hi
      exact ZMod.eq_one_of_isUnit_natCast hi
    exact Rat.intCast_injective (by simp [Algebra.coe_norm_int, ‚Üê Algebra.norm_algebraMap])
  ¬∑ rcases abs_choice p with h | h <;>
    simpa [h, span_singleton_neg p, ‚Üê submodule_span_eq, ‚Üê hp] using over_under P

theorem _root_.RingOfIntegers.isPrincipalIdealRing_of_abs_discr_lt
    (h : |discr K| < (2 * (œÄ / 4) ^ nrComplexPlaces K *
      ((finrank ‚Ñö K) ^ (finrank ‚Ñö K) / (finrank ‚Ñö K)!)) ^ 2) :
    IsPrincipalIdealRing (ùìû K) := by
  have : 0 < finrank ‚Ñö K := finrank_pos -- Lean needs to know this for `positivity` to succeed
  rw [‚Üê Real.sqrt_lt (by positivity) (by positivity), mul_assoc, ‚Üê inv_mul_lt_iff‚ÇÄ' (by positivity),
    mul_inv, ‚Üê inv_pow, inv_div, inv_div, mul_assoc, Int.cast_abs] at h
  refine RingOfIntegers.isPrincipalIdealRing_of_isPrincipal_of_norm_le (fun I hI ‚Ü¶ ?_)
  rw [absNorm_eq_one_iff.mp <| le_antisymm (lt_succ.mp (cast_lt.mp
    (lt_of_le_of_lt hI h))) <| one_le_iff_ne_zero.mpr (absNorm_ne_zero_of_nonZeroDivisors I)]
  exact top_isPrincipal

end NumberField

namespace Rat

open NumberField

theorem classNumber_eq : NumberField.classNumber ‚Ñö = 1 :=
  classNumber_eq_one_iff.mpr <| IsPrincipalIdealRing.of_surjective
    Rat.ringOfIntegersEquiv.symm Rat.ringOfIntegersEquiv.symm.surjective

end Rat
