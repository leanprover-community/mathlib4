/-
Copyright (c) 2024 Michail Karatarakis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michail Karatarakis
-/
import Mathlib.NumberTheory.NumberField.CanonicalEmbedding.Basic

/-!

# Reindexed basis
This file introduces an equivalence between the set of embeddings of `K` into `â„‚` and the
index set of the chosen basis of the ring of integers of `K`.

## Tags

house, number field, algebraic number
-/

variable (K : Type*) [Field K] [NumberField K]

namespace NumberField

noncomputable section

open Module.Free Module canonicalEmbedding Matrix Finset

/-- An equivalence between the set of embeddings of `K` into `â„‚` and the
  index set of the chosen basis of the ring of integers of `K`. -/
abbrev equivReindex : (K â†’+* â„‚) â‰ƒ ChooseBasisIndex â„¤ (ğ“ K) :=
  Fintype.equivOfCardEq <| by
    rw [Embeddings.card, â† finrank_eq_card_chooseBasisIndex, RingOfIntegers.rank]

/-- The basis matrix for the embeddings of `K` into `â„‚`. This matrix is formed by
  taking the lattice basis vectors of `K` and reindexing them according to the
  equivalence `equivReindex`, then transposing the resulting matrix. -/
abbrev basisMatrix : Matrix (K â†’+* â„‚) (K â†’+* â„‚) â„‚ :=
  (Matrix.of fun i â†¦ latticeBasis K (equivReindex K i))

theorem det_of_basisMatrix_non_zero [DecidableEq (K â†’+* â„‚)] : (basisMatrix K).det â‰  0 := by
  let e : (K â†’+* â„‚) â‰ƒ ChooseBasisIndex â„¤ (ğ“ K) := equivReindex K
  let N := Algebra.embeddingsMatrixReindex â„š â„‚ (fun i => integralBasis K (e i))
    RingHom.equivRatAlgHom
  rw [show (basisMatrix K) = N by
    ext:2; simp only [N, transpose_apply, latticeBasis_apply, integralBasis_apply,
    of_apply, apply_at]; rfl, â† pow_ne_zero_iff two_ne_zero]
  convert (map_ne_zero_iff _ (algebraMap â„š â„‚).injective).mpr
    (Algebra.discr_not_zero_of_basis â„š (integralBasis K))
  rw [â† Algebra.discr_reindex â„š (integralBasis K) e.symm]
  exact (Algebra.discr_eq_det_embeddingsMatrixReindex_pow_two â„š â„‚
    (fun _ => integralBasis K (e _)) RingHom.equivRatAlgHom).symm

instance [DecidableEq (K â†’+* â„‚)] : Invertible (basisMatrix K) := invertibleOfIsUnitDet _
    (Ne.isUnit (det_of_basisMatrix_non_zero K))

variable {K}

theorem canonicalEmbedding_eq_basisMatrix_mulVec (Î± : K) :
    canonicalEmbedding K Î± = (basisMatrix K).transpose.mulVec
      (fun i â†¦ (((integralBasis K).reindex (equivReindex K).symm).repr Î± i : â„‚)) := by
  ext i
  rw [â† (latticeBasis K).sum_repr (canonicalEmbedding K Î±), â† Equiv.sum_comp (equivReindex K)]
  simp only [canonicalEmbedding.integralBasis_repr_apply, mulVec, dotProduct,
    transpose_apply, of_apply, Fintype.sum_apply, mul_comm, Basis.repr_reindex,
    Finsupp.mapDomain_equiv_apply, Equiv.symm_symm, Pi.smul_apply, smul_eq_mul]

theorem inverse_basisMatrix_mulVec_eq_repr [DecidableEq (K â†’+* â„‚)] (Î± : ğ“ K) :
    âˆ€ i, ((basisMatrix K).transpose)â»Â¹.mulVec (fun j =>
      canonicalEmbedding K (algebraMap (ğ“ K) K Î±) j) i =
      ((integralBasis K).reindex (equivReindex K).symm).repr Î± i := fun i => by
  rw [inv_mulVec_eq_vec (canonicalEmbedding_eq_basisMatrix_mulVec ((algebraMap (ğ“ K) K) Î±))]

end

end NumberField
