import Mathlib.NumberTheory.Cyclotomic.Rat
import Mathlib.NumberTheory.KroneckerWeber.Cyclotomic.UnitLemmas
import Mathlib.NumberTheory.KroneckerWeber.Cyclotomic.CyclRat
import Mathlib.RingTheory.Ideal.Norm.AbsNorm
import Mathlib.RingTheory.NormTrace

variable {K : Type*} {p : â„•+} [hpri : Fact p.Prime] [Field K] [CharZero K]
  [IsCyclotomicExtension {p} â„š K]

variable {Î¶ : K} (hÎ¶ : IsPrimitiveRoot Î¶ p)

open scoped BigOperators nonZeroDivisors NumberField
open Polynomial

variable (hp : p â‰  2)

lemma IsPrimitiveRoot.prime_span_sub_one :
    Prime (Ideal.span <| singleton <| (hÎ¶.unit' - 1 : ð“ž K)) := by
  haveI : Fact (Nat.Prime p) := hpri
  letI := IsCyclotomicExtension.numberField {p} â„š K
  rw [Ideal.prime_iff_isPrime,
    Ideal.span_singleton_prime (hÎ¶.unit'_coe.sub_one_ne_zero hpri.out.one_lt)]
  Â· exact hÎ¶.zeta_sub_one_prime'
  Â· rw [Ne, Ideal.span_singleton_eq_bot]
    exact hÎ¶.unit'_coe.sub_one_ne_zero hpri.out.one_lt

omit [CharZero K] [IsCyclotomicExtension {p} â„š K] in
lemma associated_zeta_sub_one_pow_prime [NumberField K] :
    Associated ((hÎ¶.unit' - 1 : ð“ž K) ^ (p - 1 : â„•)) p := by
  haveI : Fact (Nat.Prime p) := hpri
  rw [â† eval_one_cyclotomic_prime (R := ð“ž K) (p := p),
    cyclotomic_eq_prod_X_sub_primitiveRoots hÎ¶.unit'_coe, eval_prod]
  simp only [eval_sub, eval_X, eval_C]
  rw [â† Nat.totient_prime this.out, â† hÎ¶.unit'_coe.card_primitiveRoots, â† Finset.prod_const]
  apply Associated.prod
  intro Î· hÎ·
  exact hÎ¶.unit'_coe.associated_sub_one hpri.out
    (one_mem_nthRootsFinset this.out.pos)
    ((isPrimitiveRoot_of_mem_primitiveRoots hÎ·).mem_nthRootsFinset hpri.out.pos)
      ((isPrimitiveRoot_of_mem_primitiveRoots hÎ·).ne_one hpri.out.one_lt).symm

lemma isCoprime_of_not_zeta_sub_one_dvd {x : ð“ž K} (hx : Â¬ (hÎ¶.unit' : ð“ž K) - 1 âˆ£ x) :
    IsCoprime â†‘p x := by
  letI := IsCyclotomicExtension.numberField {p} â„š K
  rwa [â† Ideal.isCoprime_span_singleton_iff,
    â† Ideal.span_singleton_eq_span_singleton.mpr (associated_zeta_sub_one_pow_prime hÎ¶),
    â† Ideal.span_singleton_pow, IsCoprime.pow_left_iff, Ideal.isCoprime_iff_gcd,
    hÎ¶.prime_span_sub_one.irreducible.gcd_eq_one_iff, Ideal.dvd_span_singleton,
    Ideal.mem_span_singleton]
  Â· simpa only [ge_iff_le, tsub_pos_iff_lt] using hpri.out.one_lt

set_option synthInstance.maxHeartbeats 40000 in
lemma exists_zeta_sub_one_dvd_sub_Int (a : ð“ž K) : âˆƒ b : â„¤, (hÎ¶.unit' - 1 : ð“ž K) âˆ£ a - b := by
  letI : Fact (Nat.Prime p) := hpri
  simp_rw [â† Ideal.Quotient.eq_zero_iff_dvd, â† Ideal.Quotient.mk_eq_mk, Submodule.Quotient.mk_sub,
    sub_eq_zero, â† SModEq.def]
  exact hÎ¶.subOneIntegralPowerBasis'_gen â–¸ hÎ¶.subOneIntegralPowerBasis'.exists_smodEq a

include hp in
lemma exists_dvd_pow_sub_Int_pow (a : ð“ž K) : âˆƒ b : â„¤, â†‘p âˆ£ a ^ (p : â„•) - (b : ð“ž K) ^ (p : â„•) := by
  obtain âŸ¨Î¶, hÎ¶âŸ© := IsCyclotomicExtension.exists_prim_root â„š (B := K) (Set.mem_singleton p)
  obtain âŸ¨b, k, eâŸ© := exists_zeta_sub_one_dvd_sub_Int hÎ¶ a
  obtain âŸ¨r, hrâŸ© := exists_add_pow_prime_eq hpri.out a (-b)
  letI := IsCyclotomicExtension.numberField {p} â„š K
  obtain âŸ¨u, huâŸ© := (associated_zeta_sub_one_pow_prime hÎ¶).symm
  rw [(Nat.Prime.odd_of_ne_two hpri.out (PNat.coe_injective.ne hp)).neg_pow, â† sub_eq_add_neg, e,
    mul_pow, â† sub_eq_add_neg] at hr
  nth_rw 1 [â† Nat.sub_add_cancel (n := p) (m := 1) hpri.out.one_lt.le] at hr
  rw [pow_succ, â† hu, mul_assoc, mul_assoc] at hr
  use b, â†‘u * ((hÎ¶.unit' - 1 : ð“ž K) * k ^ (p : â„•)) - r
  rw [mul_sub, hr, add_sub_cancel_right]

section

variable {Î±} [CommMonoidWithZero Î±]

theorem prime_units_mul (a : Î±Ë£) (b : Î±) : Prime (â†‘a * b) â†” Prime b := by simp [Prime]

end

lemma zeta_sub_one_dvd_Int_iff {n : â„¤} : (hÎ¶.unit' : ð“ž K) - 1 âˆ£ n â†” â†‘p âˆ£ n := by
  letI := IsCyclotomicExtension.numberField {p} â„š K
  by_cases hp : p = 2
  Â· subst hp
    have : IsCyclotomicExtension {2 ^ (0 + 1)} â„š K := by rwa [zero_add, pow_one]
    have hÎ¶' : IsPrimitiveRoot Î¶ â†‘((2 : â„•+) ^ (0 + 1)) := by simpa
    have := hÎ¶'.norm_toInteger_pow_sub_one_of_two
    rw [pow_zero, pow_one, pow_one (-2)] at this
    replace this : (Algebra.norm â„¤) (hÎ¶.toInteger - 1) = -2 := this
    simp only [PNat.val_ofNat, Nat.cast_ofNat]
    rw [â† neg_dvd (a := (2 : â„¤)), â† this, Ideal.norm_dvd_iff]
    Â· rfl
    Â· rw [this]
      refine Prime.neg Int.prime_two
  rw [â† hÎ¶.norm_toInteger_sub_one_of_prime_ne_two' hp, Ideal.norm_dvd_iff]
  Â· rfl
  Â· rw [hÎ¶.norm_toInteger_sub_one_of_prime_ne_two' hp,  â† Nat.prime_iff_prime_int]
    exact hpri.1

lemma IsPrimitiveRoot.sub_one_dvd_sub {A : Type*} [CommRing A] [IsDomain A]
    {p : â„•} (hp : p.Prime) {Î¶ : A} (hÎ¶ : IsPrimitiveRoot Î¶ p) {Î·â‚ : A}
    (hÎ·â‚ : Î·â‚ âˆˆ nthRootsFinset p A) {Î·â‚‚ : A} (hÎ·â‚‚ : Î·â‚‚ âˆˆ nthRootsFinset p A) :
    Î¶ - 1 âˆ£ Î·â‚ - Î·â‚‚ := by
  by_cases h : Î·â‚ = Î·â‚‚
  Â· rw [h, sub_self]; exact dvd_zero _
  Â· exact (hÎ¶.associated_sub_one hp hÎ·â‚ hÎ·â‚‚ h).dvd

set_option synthInstance.maxHeartbeats 40000 in
lemma quotient_zero_sub_one_comp_aut (Ïƒ : ð“ž K â†’+* ð“ž K) :
    (Ideal.Quotient.mk (Ideal.span {(hÎ¶.unit' : ð“ž K) - 1})).comp Ïƒ = Ideal.Quotient.mk _ := by
  have : Fact (Nat.Prime p) := hpri
  letI := IsCyclotomicExtension.numberField {p} â„š K
  letI : AddGroup (ð“ž K â§¸ Ideal.span (singleton (hÎ¶.unit' - 1: ð“ž K))) := inferInstance
  apply RingHom.toIntAlgHom_injective
  apply hÎ¶.integralPowerBasis'.algHom_ext
  have h : hÎ¶.integralPowerBasis'.gen = hÎ¶.unit' := by
    simp only [IsPrimitiveRoot.integralPowerBasis'_gen]
    rfl
  rw [h]
  simp only [RingHom.toIntAlgHom, RingHom.toMonoidHom_eq_coe, AlgHom.coe_mk, RingHom.coe_mk,
    MonoidHom.coe_coe, RingHom.coe_comp, RingHom.coe_coe, Function.comp_apply]
  rw [â† sub_eq_zero, â† Ideal.Quotient.mk_eq_mk, â† Ideal.Quotient.mk_eq_mk,
    â† Submodule.Quotient.mk_sub, Ideal.Quotient.mk_eq_mk, Ideal.Quotient.eq_zero_iff_mem,
    Ideal.mem_span_singleton]
  apply hÎ¶.unit'_coe.sub_one_dvd_sub hpri.out
  Â· rw [mem_nthRootsFinset p.pos, â† map_pow, hÎ¶.unit'_coe.pow_eq_one, map_one]
  Â· rw [mem_nthRootsFinset p.pos, hÎ¶.unit'_coe.pow_eq_one]

set_option maxHeartbeats 400000 in
set_option synthInstance.maxHeartbeats 80000 in
lemma zeta_sub_one_dvd_trace_sub_smul (x : ð“ž K) :
    (hÎ¶.unit' - 1 : ð“ž K) âˆ£ Algebra.trace â„¤ _ x - (p - 1 : â„•) â€¢ x := by
  letI := IsCyclotomicExtension.numberField {p} â„š K
  letI := IsCyclotomicExtension.isGalois p â„š K
  have : (Algebra.trace â„¤ _ x : ð“ž K) = âˆ‘ Ïƒ : K â‰ƒâ‚[â„š] K, (intGal Ïƒ).toRingHom x := by
    apply (show Function.Injective (algebraMap (ð“ž K) K) from Subtype.val_injective)
    rw [â† eq_intCast (algebraMap â„¤ (ð“ž K)), â† IsScalarTower.algebraMap_apply,
      IsScalarTower.algebraMap_apply â„¤ â„š K, eq_intCast, Algebra.coe_trace_int,
      trace_eq_sum_automorphisms, map_sum]
    rfl
  rw [â† Ideal.mem_span_singleton, â† Ideal.Quotient.eq_zero_iff_mem, map_sub, this,
    map_sum]
  simp_rw [â† RingHom.comp_apply, quotient_zero_sub_one_comp_aut]
  rw [Finset.sum_const, map_nsmul, sub_eq_zero, Finset.card_univ, IsGalois.card_aut_eq_finrank,
    IsCyclotomicExtension.finrank K (cyclotomic.irreducible_rat p.pos), Nat.totient_prime hpri.out]

lemma zeta_sub_one_pow_dvd_norm_sub_pow (x : ð“ž K) :
    (hÎ¶.unit' - 1 : ð“ž K) ^ (p : â„•) âˆ£
      (Algebra.norm â„¤ (1 + (p : â„•) â€¢ x) : ð“ž K) - 1 + (p : â„•) â€¢ x := by
  letI := IsCyclotomicExtension.numberField {p} â„š K
  obtain âŸ¨r, hrâŸ© := Algebra.norm_one_add_smul (p : â„¤) x
  obtain âŸ¨s, hsâŸ© := zeta_sub_one_dvd_trace_sub_smul hÎ¶ x
  obtain âŸ¨t, htâŸ© := (associated_zeta_sub_one_pow_prime hÎ¶).dvd
  rw [sub_eq_iff_eq_add] at hs
  simp only [zsmul_eq_mul, Int.cast_natCast] at hr
  simp only [nsmul_eq_mul, hr, Int.cast_add, Int.cast_one, Int.cast_mul, hs, ge_iff_le, PNat.pos,
    Nat.cast_pred, Int.cast_pow, Nat.cast_mul, ne_eq, PNat.ne_zero, Int.cast_natCast,
    not_false_eq_true, isUnit_pow_iff]
  suffices (hÎ¶.unit' - 1 : ð“ž K) ^ (p : â„•) âˆ£ (hÎ¶.unit' - 1) * p * s + (p : ð“ž K) ^ 2 * (r + x) by
    convert this using 1; ring
  apply dvd_add
  Â· apply dvd_mul_of_dvd_left
    rw [ht, â† mul_assoc, â† pow_succ', tsub_add_cancel_of_le (Nat.Prime.one_lt hpri.out).le]
    exact dvd_mul_right _ _
  Â· rw [ht, mul_pow, â† pow_mul, mul_assoc]
    apply dvd_mul_of_dvd_left
    apply pow_dvd_pow
    zify [(Nat.Prime.one_lt hpri.out).le]
    linarith only [Nat.Prime.two_le hpri.out]

lemma norm_add_one_smul_of_isUnit {K} [Field K] [NumberField K] {p : â„•} (hpri : p.Prime)
    (hp : p â‰  2) (x : ð“ž K)
    (hx : IsUnit (1 + (p : â„•) â€¢ x)) : Algebra.norm â„¤ (1 + (p : â„•) â€¢ x) = 1 := by
  have H : Algebra.norm â„¤ (1 + (p : â„•) â€¢ x) = 1 âˆ¨ Algebra.norm â„¤ (1 + (p : â„•) â€¢ x) = -1 := by
    apply Int.natAbs_eq_iff.mp
    apply (Int.cast_injective (Î± := â„š)).comp Nat.cast_injective
    simp only [Int.cast_abs, Function.comp_apply, Nat.cast_one, Int.cast_one, â† Int.abs_eq_natAbs,
      Algebra.coe_norm_int, â† NumberField.isUnit_iff_norm.mp hx, RingOfIntegers.coe_norm]
  have : Algebra.norm â„¤ (1 + (p : â„•) â€¢ x) â‰  -1 := by
    intro e; apply hp
    obtain âŸ¨r, hrâŸ© := Algebra.norm_one_add_smul (p : â„¤) x
    have : (p : â„¤) * (- Algebra.trace â„¤ _ x - r * p) = 2 := by
      rw [zsmul_eq_mul, Int.cast_natCast, â† nsmul_eq_mul, e, eq_comm, â† sub_eq_zero] at hr
      rw [eq_comm, â† sub_eq_zero, â† hr]
      ring
    exact (Nat.prime_two.eq_one_or_self_of_dvd _
      (Int.natCast_dvd_natCast.mp âŸ¨_, this.symmâŸ©)).resolve_left hpri.ne_one
  exact H.resolve_right this
