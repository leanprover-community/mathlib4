/-
Copyright (c) 2022 Riccardo Brasca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Riccardo Brasca
-/
import Mathlib.NumberTheory.Cyclotomic.Discriminant
import Mathlib.RingTheory.Polynomial.Eisenstein.IsIntegral
import Mathlib.RingTheory.Ideal.Norm

#align_import number_theory.cyclotomic.rat from "leanprover-community/mathlib"@"b353176c24d96c23f0ce1cc63efc3f55019702d9"

/-!
# Ring of integers of `p ^ n`-th cyclotomic fields
We gather results about cyclotomic extensions of `â„š`. In particular, we compute the ring of
integers of a `p ^ n`-th cyclotomic extension of `â„š`.

## Main results
* `IsCyclotomicExtension.Rat.isIntegralClosure_adjoin_singleton_of_prime_pow`: if `K` is a
  `p ^ k`-th cyclotomic extension of `â„š`, then `(adjoin â„¤ {Î¶})` is the integral closure of
  `â„¤` in `K`.
* `IsCyclotomicExtension.Rat.cyclotomicRing_isIntegralClosure_of_prime_pow`: the integral
  closure of `â„¤` inside `CyclotomicField (p ^ k) â„š` is `CyclotomicRing (p ^ k) â„¤ â„š`.
* `IsCyclotomicExtension.Rat.absdiscr_prime_pow` and related results: the absolute discriminant
  of cyclotomic fields.
-/

universe u

open Algebra IsCyclotomicExtension Polynomial NumberField

open scoped Cyclotomic NumberField Nat

variable {p : â„•+} {k : â„•} {K : Type u} [Field K] [CharZero K] {Î¶ : K} [hp : Fact (p : â„•).Prime]

-- These are to speed up instance searches
@[local instance] private def instAlg : Algebra â„¤ (adjoin â„¤ ({Î¶} : Set K)) :=
  Subalgebra.algebra (adjoin â„¤ {Î¶})
@[local instance] private def instAlg' : Algebra (adjoin â„¤ ({Î¶} : Set K)) K :=
  Subalgebra.toAlgebra (adjoin â„¤ {Î¶})
@[local instance] private def instFinite : Finite ({p ^ k} : Set â„•+) := Finite.of_fintype _
@[local instance] private def instNontrivial_K : Nontrivial K := âŸ¨0, 1, zero_ne_oneâŸ©
@[local instance] private noncomputable def instModule_cyc :
    Module (CyclotomicRing (p ^ k) â„¤ â„š) (CyclotomicField (p ^ k) â„š) := toModule
@[local instance] private def instAlg'' : Algebra â„¤ (ğ“ K) := Subalgebra.algebra (ğ“ K)
@[local instance] private def instAlg''' : Algebra (ğ“ K) K := Subalgebra.toAlgebra _
@[local instance] private lemma instPID_Int : IsPrincipalIdealRing â„¤ :=
  EuclideanDomain.to_principal_ideal_domain
@[local instance] private lemma instIntegrallyClosed_Int : IsIntegrallyClosed â„¤ :=
  UniqueFactorizationMonoid.instIsIntegrallyClosed
@[local instance] private def instAddCommGroup [NumberField K] : AddCommGroup â†¥(ğ“ K) :=
  Ring.toAddCommGroup
@[local instance] private def instModFin [NumberField K] : Module.Finite â„¤ â†¥(ğ“ K) :=
  Module.IsNoetherian.finite â„¤ â†¥(ğ“ K)
@[local instance] private def instAMHC : AddMonoidHomClass (â†¥(ğ“ K) â†’+* K) (â†¥(ğ“ K)) K :=
  RingHomClass.toAddMonoidHomClass
@[local instance] private def instMHC : MonoidHomClass (â†¥(ğ“ K) â†’+* K) (â†¥(ğ“ K)) K :=
  RingHomClass.toMonoidHomClass

namespace IsCyclotomicExtension.Rat

/-- The discriminant of the power basis given by `Î¶ - 1`. -/
theorem discr_prime_pow_ne_two' [IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(p ^ (k + 1))) (hk : p ^ (k + 1) â‰  2) :
    discr â„š (hÎ¶.subOnePowerBasis â„š).basis =
      (-1) ^ ((p ^ (k + 1) : â„•).totient / 2) * p ^ ((p : â„•) ^ k * ((p - 1) * (k + 1) - 1)) := by
  rw [â† discr_prime_pow_ne_two hÎ¶ (cyclotomic.irreducible_rat (p ^ (k + 1)).pos) hk]
  exact hÎ¶.discr_zeta_eq_discr_zeta_sub_one.symm
#align is_cyclotomic_extension.rat.discr_prime_pow_ne_two' IsCyclotomicExtension.Rat.discr_prime_pow_ne_two'

theorem discr_odd_prime' [IsCyclotomicExtension {p} â„š K] (hÎ¶ : IsPrimitiveRoot Î¶ p) (hodd : p â‰  2) :
    discr â„š (hÎ¶.subOnePowerBasis â„š).basis = (-1) ^ (((p : â„•) - 1) / 2) * p ^ ((p : â„•) - 2) := by
  rw [â† discr_odd_prime hÎ¶ (cyclotomic.irreducible_rat hp.out.pos) hodd]
  exact hÎ¶.discr_zeta_eq_discr_zeta_sub_one.symm
#align is_cyclotomic_extension.rat.discr_odd_prime' IsCyclotomicExtension.Rat.discr_odd_prime'

/-- The discriminant of the power basis given by `Î¶ - 1`. Beware that in the cases `p ^ k = 1` and
`p ^ k = 2` the formula uses `1 / 2 = 0` and `0 - 1 = 0`. It is useful only to have a uniform
result. See also `IsCyclotomicExtension.Rat.discr_prime_pow_eq_unit_mul_pow'`. -/
theorem discr_prime_pow' [IsCyclotomicExtension {p ^ k} â„š K] (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(p ^ k)) :
    discr â„š (hÎ¶.subOnePowerBasis â„š).basis =
      (-1) ^ ((p ^ k : â„•).totient / 2) * p ^ ((p : â„•) ^ (k - 1) * ((p - 1) * k - 1)) := by
  rw [â† discr_prime_pow hÎ¶ (cyclotomic.irreducible_rat (p ^ k).pos)]
  exact hÎ¶.discr_zeta_eq_discr_zeta_sub_one.symm
#align is_cyclotomic_extension.rat.discr_prime_pow' IsCyclotomicExtension.Rat.discr_prime_pow'

/-- If `p` is a prime and `IsCyclotomicExtension {p ^ k} K L`, then there are `u : â„¤Ë£` and
`n : â„•` such that the discriminant of the power basis given by `Î¶ - 1` is `u * p ^ n`. Often this is
enough and less cumbersome to use than `IsCyclotomicExtension.Rat.discr_prime_pow'`. -/
theorem discr_prime_pow_eq_unit_mul_pow' [IsCyclotomicExtension {p ^ k} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(p ^ k)) :
    âˆƒ (u : â„¤Ë£) (n : â„•), discr â„š (hÎ¶.subOnePowerBasis â„š).basis = u * p ^ n := by
  rw [hÎ¶.discr_zeta_eq_discr_zeta_sub_one.symm]
  exact discr_prime_pow_eq_unit_mul_pow hÎ¶ (cyclotomic.irreducible_rat (p ^ k).pos)
#align is_cyclotomic_extension.rat.discr_prime_pow_eq_unit_mul_pow' IsCyclotomicExtension.Rat.discr_prime_pow_eq_unit_mul_pow'

/-- If `K` is a `p ^ k`-th cyclotomic extension of `â„š`, then `(adjoin â„¤ {Î¶})` is the
integral closure of `â„¤` in `K`. -/
theorem isIntegralClosure_adjoin_singleton_of_prime_pow [hcycl : IsCyclotomicExtension {p ^ k} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(p ^ k)) : IsIntegralClosure (adjoin â„¤ ({Î¶} : Set K)) â„¤ K := by
  refine' âŸ¨Subtype.val_injective, @fun x => âŸ¨fun h => âŸ¨âŸ¨x, _âŸ©, rflâŸ©, _âŸ©âŸ©
  swap
  Â· rintro âŸ¨y, rflâŸ©
    exact
      IsIntegral.algebraMap
        (le_integralClosure_iff_isIntegral.1
          (adjoin_le_integralClosure (hÎ¶.isIntegral (p ^ k).pos)) _)
  let B := hÎ¶.subOnePowerBasis â„š
  have hint : IsIntegral â„¤ B.gen := (hÎ¶.isIntegral (p ^ k).pos).sub isIntegral_one
-- Porting note: the following `haveI` was not needed because the locale `cyclotomic` set it
-- as instances.
  letI := IsCyclotomicExtension.finiteDimensional {p ^ k} â„š K
  have H := discr_mul_isIntegral_mem_adjoin â„š hint h
  obtain âŸ¨u, n, hunâŸ© := discr_prime_pow_eq_unit_mul_pow' hÎ¶
  rw [hun] at H
  replace H := Subalgebra.smul_mem _ H u.inv
-- Porting note: the proof is slightly different because of coercions.
  rw [â† smul_assoc, â† smul_mul_assoc, Units.inv_eq_val_inv, zsmul_eq_mul, â† Int.cast_mul,
    Units.inv_mul, Int.cast_one, one_mul, smul_def, map_pow] at H
  cases k
  Â· haveI : IsCyclotomicExtension {1} â„š K := by simpa using hcycl
    have : x âˆˆ (âŠ¥ : Subalgebra â„š K) := by
      rw [singleton_one â„š K]
      exact mem_top
    obtain âŸ¨y, rflâŸ© := mem_bot.1 this
    replace h := (isIntegral_algebraMap_iff (algebraMap â„š K).injective).1 h
    obtain âŸ¨z, hzâŸ© := IsIntegrallyClosed.isIntegral_iff.1 h
    rw [â† hz, â† IsScalarTower.algebraMap_apply]
    exact Subalgebra.algebraMap_mem _ _
  Â· have hmin : (minpoly â„¤ B.gen).IsEisensteinAt (Submodule.span â„¤ {((p : â„•) : â„¤)}) := by
      have hâ‚ := minpoly.isIntegrallyClosed_eq_field_fractions' â„š hint
      have hâ‚‚ := hÎ¶.minpoly_sub_one_eq_cyclotomic_comp (cyclotomic.irreducible_rat (p ^ _).pos)
      rw [IsPrimitiveRoot.subOnePowerBasis_gen] at hâ‚
      rw [hâ‚, â† map_cyclotomic_int, show Int.castRingHom â„š = algebraMap â„¤ â„š by rfl,
        show X + 1 = map (algebraMap â„¤ â„š) (X + 1) by simp, â† map_comp] at hâ‚‚
      haveI : CharZero â„š := StrictOrderedSemiring.to_charZero
      rw [IsPrimitiveRoot.subOnePowerBasis_gen,
        map_injective (algebraMap â„¤ â„š) (algebraMap â„¤ â„š).injective_int hâ‚‚]
      exact cyclotomic_prime_pow_comp_X_add_one_isEisensteinAt p _
    refine'
      adjoin_le _
        (mem_adjoin_of_smul_prime_pow_smul_of_minpoly_isEisensteinAt (n := n)
          (Nat.prime_iff_prime_int.1 hp.out) hint h (by simpa using H) hmin)
    simp only [Set.singleton_subset_iff, SetLike.mem_coe]
    exact Subalgebra.sub_mem _ (self_mem_adjoin_singleton â„¤ _) (Subalgebra.one_mem _)
#align is_cyclotomic_extension.rat.is_integral_closure_adjoin_singleton_of_prime_pow IsCyclotomicExtension.Rat.isIntegralClosure_adjoin_singleton_of_prime_pow

theorem isIntegralClosure_adjoin_singleton_of_prime [hcycl : IsCyclotomicExtension {p} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘p) : IsIntegralClosure (adjoin â„¤ ({Î¶} : Set K)) â„¤ K := by
  rw [â† pow_one p] at hÎ¶ hcycl
  exact isIntegralClosure_adjoin_singleton_of_prime_pow hÎ¶
#align is_cyclotomic_extension.rat.is_integral_closure_adjoin_singleton_of_prime IsCyclotomicExtension.Rat.isIntegralClosure_adjoin_singleton_of_prime

/-- The integral closure of `â„¤` inside `CyclotomicField (p ^ k) â„š` is
`CyclotomicRing (p ^ k) â„¤ â„š`. -/
theorem cyclotomicRing_isIntegralClosure_of_prime_pow :
    IsIntegralClosure (CyclotomicRing (p ^ k) â„¤ â„š) â„¤ (CyclotomicField (p ^ k) â„š) := by
  haveI : CharZero â„š := StrictOrderedSemiring.to_charZero
  have hÎ¶ := zeta_spec (p ^ k) â„š (CyclotomicField (p ^ k) â„š)
  refine âŸ¨IsFractionRing.injective _ _, @fun x => âŸ¨fun h => âŸ¨âŸ¨x, ?_âŸ©, rflâŸ©, ?_âŸ©âŸ©
-- Porting note: having `.isIntegral_iff` inside the definition of `this` causes an error.
  Â· have := isIntegralClosure_adjoin_singleton_of_prime_pow hÎ¶
    obtain âŸ¨y, rflâŸ© := this.isIntegral_iff.1 h
    refine' adjoin_mono _ y.2
    simp only [PNat.pow_coe, Set.singleton_subset_iff, Set.mem_setOf_eq]
    exact hÎ¶.pow_eq_one
  Â· rintro âŸ¨y, rflâŸ©
    exact IsIntegral.algebraMap ((IsCyclotomicExtension.integral {p ^ k} â„¤ _) _)
#align is_cyclotomic_extension.rat.cyclotomic_ring_is_integral_closure_of_prime_pow IsCyclotomicExtension.Rat.cyclotomicRing_isIntegralClosure_of_prime_pow

theorem cyclotomicRing_isIntegralClosure_of_prime :
    IsIntegralClosure (CyclotomicRing p â„¤ â„š) â„¤ (CyclotomicField p â„š) := by
  rw [â† pow_one p]
  exact cyclotomicRing_isIntegralClosure_of_prime_pow
#align is_cyclotomic_extension.rat.cyclotomic_ring_is_integral_closure_of_prime IsCyclotomicExtension.Rat.cyclotomicRing_isIntegralClosure_of_prime

end IsCyclotomicExtension.Rat

section PowerBasis

open IsCyclotomicExtension.Rat

namespace IsPrimitiveRoot

instance : Algebra â„¤ (ğ“ K) := Subalgebra.algebra (ğ“ K)
instance : Algebra (ğ“ K) K := Subalgebra.toAlgebra _

/-- The algebra isomorphism `adjoin â„¤ {Î¶} â‰ƒâ‚[â„¤] (ğ“ K)`, where `Î¶` is a primitive `p ^ k`-th root of
unity and `K` is a `p ^ k`-th cyclotomic extension of `â„š`. -/
@[simps!]
noncomputable def _root_.IsPrimitiveRoot.adjoinEquivRingOfIntegers
    [IsCyclotomicExtension {p ^ k} â„š K] (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(p ^ k)) :
    adjoin â„¤ ({Î¶} : Set K) â‰ƒâ‚[â„¤] ğ“ K :=
  let _ := isIntegralClosure_adjoin_singleton_of_prime_pow hÎ¶
  IsIntegralClosure.equiv â„¤ (adjoin â„¤ ({Î¶} : Set K)) K (ğ“ K)
#align is_primitive_root.adjoin_equiv_ring_of_integers IsPrimitiveRoot.adjoinEquivRingOfIntegers

/-- The ring of integers of a `p ^ k`-th cyclotomic extension of `â„š` is a cyclotomic extension. -/
instance IsCyclotomicExtension.ringOfIntegers [IsCyclotomicExtension {p ^ k} â„š K] :
    IsCyclotomicExtension {p ^ k} â„¤ (ğ“ K) :=
  let _ := (zeta_spec (p ^ k) â„š K).adjoin_isCyclotomicExtension â„¤
  IsCyclotomicExtension.equiv _ â„¤ _ (zeta_spec (p ^ k) â„š K).adjoinEquivRingOfIntegers
#align is_cyclotomic_extension.ring_of_integers IsPrimitiveRoot.IsCyclotomicExtension.ringOfIntegers

/-- The integral `PowerBasis` of `ğ“ K` given by a primitive root of unity, where `K` is a `p ^ k`
cyclotomic extension of `â„š`. -/
noncomputable def integralPowerBasis [IsCyclotomicExtension {p ^ k} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(p ^ k)) : PowerBasis â„¤ (ğ“ K) :=
  (Algebra.adjoin.powerBasis' (hÎ¶.isIntegral (p ^ k).pos)).map hÎ¶.adjoinEquivRingOfIntegers
#align is_primitive_root.integral_power_basis IsPrimitiveRoot.integralPowerBasis

/-- Abbreviation to see a primitive root of unity as a member of the ring of integers. -/
abbrev toInteger {k : â„•+} (hÎ¶ : IsPrimitiveRoot Î¶ k) : ğ“ K := âŸ¨Î¶, hÎ¶.isIntegral k.posâŸ©

-- Porting note: the proof changed because `simp` unfolds too much.
@[simp]
theorem integralPowerBasis_gen [hcycl : IsCyclotomicExtension {p ^ k} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(p ^ k)) :
    hÎ¶.integralPowerBasis.gen = hÎ¶.toInteger :=
  Subtype.ext <| show algebraMap _ K hÎ¶.integralPowerBasis.gen = _ by
    rw [integralPowerBasis, PowerBasis.map_gen, adjoin.powerBasis'_gen]
    simp only [adjoinEquivRingOfIntegers_apply, IsIntegralClosure.algebraMap_lift]
    rfl
#align is_primitive_root.integral_power_basis_gen IsPrimitiveRoot.integralPowerBasis_gen

@[simp]
theorem integralPowerBasis_dim [hcycl : IsCyclotomicExtension {p ^ k} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(p ^ k)) : hÎ¶.integralPowerBasis.dim = Ï† (p ^ k) := by
  simp [integralPowerBasis, â† cyclotomic_eq_minpoly hÎ¶, natDegree_cyclotomic]
#align is_primitive_root.integral_power_basis_dim IsPrimitiveRoot.integralPowerBasis_dim

/-- The algebra isomorphism `adjoin â„¤ {Î¶} â‰ƒâ‚[â„¤] (ğ“ K)`, where `Î¶` is a primitive `p`-th root of
unity and `K` is a `p`-th cyclotomic extension of `â„š`. -/
@[simps!]
noncomputable def _root_.IsPrimitiveRoot.adjoinEquivRingOfIntegers'
    [hcycl : IsCyclotomicExtension {p} â„š K] (hÎ¶ : IsPrimitiveRoot Î¶ p) :
    adjoin â„¤ ({Î¶} : Set K) â‰ƒâ‚[â„¤] ğ“ K :=
  @adjoinEquivRingOfIntegers p 1 K _ _ _ _ (by convert hcycl; rw [pow_one]) (by rwa [pow_one])
#align is_primitive_root.adjoin_equiv_ring_of_integers' IsPrimitiveRoot.adjoinEquivRingOfIntegers'

/-- The ring of integers of a `p`-th cyclotomic extension of `â„š` is a cyclotomic extension. -/
instance _root_.IsCyclotomicExtension.ring_of_integers' [IsCyclotomicExtension {p} â„š K] :
    IsCyclotomicExtension {p} â„¤ (ğ“ K) :=
  let _ := (zeta_spec p â„š K).adjoin_isCyclotomicExtension â„¤
  IsCyclotomicExtension.equiv _ â„¤ _ (zeta_spec p â„š K).adjoinEquivRingOfIntegers'
#align is_cyclotomic_extension.ring_of_integers' IsCyclotomicExtension.ring_of_integers'

/-- The integral `PowerBasis` of `ğ“ K` given by a primitive root of unity, where `K` is a `p`-th
cyclotomic extension of `â„š`. -/
noncomputable def integralPowerBasis' [hcycl : IsCyclotomicExtension {p} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ p) : PowerBasis â„¤ (ğ“ K) :=
  @integralPowerBasis p 1 K _ _ _ _ (by convert hcycl; rw [pow_one]) (by rwa [pow_one])
#align is_primitive_root.integral_power_basis' IsPrimitiveRoot.integralPowerBasis'

@[simp]
theorem integralPowerBasis'_gen [hcycl : IsCyclotomicExtension {p} â„š K] (hÎ¶ : IsPrimitiveRoot Î¶ p) :
    hÎ¶.integralPowerBasis'.gen = hÎ¶.toInteger :=
  @integralPowerBasis_gen p 1 K _ _ _ _ (by convert hcycl; rw [pow_one]) (by rwa [pow_one])
#align is_primitive_root.integral_power_basis'_gen IsPrimitiveRoot.integralPowerBasis'_gen

@[simp]
theorem power_basis_int'_dim [hcycl : IsCyclotomicExtension {p} â„š K] (hÎ¶ : IsPrimitiveRoot Î¶ p) :
    hÎ¶.integralPowerBasis'.dim = Ï† p := by
  erw [@integralPowerBasis_dim p 1 K _ _ _ _ (by convert hcycl; rw [pow_one]) (by rwa [pow_one]),
    pow_one]
#align is_primitive_root.power_basis_int'_dim IsPrimitiveRoot.power_basis_int'_dim

/-- The integral `PowerBasis` of `ğ“ K` given by `Î¶ - 1`, where `K` is a `p ^ k` cyclotomic
extension of `â„š`. -/
noncomputable def subOneIntegralPowerBasis [IsCyclotomicExtension {p ^ k} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(p ^ k)) : PowerBasis â„¤ (ğ“ K) :=
  PowerBasis.ofGenMemAdjoin' hÎ¶.integralPowerBasis
    (isIntegral_of_mem_ringOfIntegers <|
      Subalgebra.sub_mem _ (hÎ¶.isIntegral (p ^ k).pos) (Subalgebra.one_mem _))
    (by
      simp only [integralPowerBasis_gen, toInteger]
      convert Subalgebra.add_mem _ (self_mem_adjoin_singleton â„¤ (âŸ¨Î¶ - 1, _âŸ© : ğ“ K))
        (Subalgebra.one_mem _)
-- Porting note: `simp` was able to finish the proof.
      simp only [Subsemiring.coe_add, Subalgebra.coe_toSubsemiring,
        OneMemClass.coe_one, sub_add_cancel]
      exact Subalgebra.sub_mem _ (hÎ¶.isIntegral (by simp)) (Subalgebra.one_mem _))
#align is_primitive_root.sub_one_integral_power_basis IsPrimitiveRoot.subOneIntegralPowerBasis

@[simp]
theorem subOneIntegralPowerBasis_gen [IsCyclotomicExtension {p ^ k} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(p ^ k)) :
    hÎ¶.subOneIntegralPowerBasis.gen =
      âŸ¨Î¶ - 1, Subalgebra.sub_mem _ (hÎ¶.isIntegral (p ^ k).pos) (Subalgebra.one_mem _)âŸ© :=
  by simp [subOneIntegralPowerBasis]
#align is_primitive_root.sub_one_integral_power_basis_gen IsPrimitiveRoot.subOneIntegralPowerBasis_gen

/-- The integral `PowerBasis` of `ğ“ K` given by `Î¶ - 1`, where `K` is a `p`-th cyclotomic
extension of `â„š`. -/
noncomputable def subOneIntegralPowerBasis' [hcycl : IsCyclotomicExtension {p} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ p) : PowerBasis â„¤ (ğ“ K) :=
  @subOneIntegralPowerBasis p 1 K _ _ _ _ (by convert hcycl; rw [pow_one]) (by rwa [pow_one])
#align is_primitive_root.sub_one_integral_power_basis' IsPrimitiveRoot.subOneIntegralPowerBasis'

@[simp]
theorem subOneIntegralPowerBasis'_gen [hcycl : IsCyclotomicExtension {p} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ p) :
    hÎ¶.subOneIntegralPowerBasis'.gen = hÎ¶.toInteger - 1 :=
  @subOneIntegralPowerBasis_gen p 1 K _ _ _ _ (by convert hcycl; rw [pow_one]) (by rwa [pow_one])
#align is_primitive_root.sub_one_integral_power_basis'_gen IsPrimitiveRoot.subOneIntegralPowerBasis'_gen

/-- `Î¶ - 1` is prime if `p â‰  2` and `Î¶` is a primitive `p ^ (k + 1)`-th root of unity.
  See `zeta_sub_one_prime` for a general statement. -/
theorem zeta_sub_one_prime_of_ne_two [IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(p ^ (k + 1))) (hodd : p â‰  2) :
    Prime (hÎ¶.toInteger - 1) := by
  letI := IsCyclotomicExtension.numberField {p ^ (k + 1)} â„š K
  refine Ideal.prime_of_irreducible_absNorm_span (fun h â†¦ ?_) ?_
  Â· apply hÎ¶.pow_ne_one_of_pos_of_lt zero_lt_one (one_lt_pow hp.out.one_lt (by simp))
    rw [â† Subalgebra.coe_eq_zero] at h
    simpa [sub_eq_zero] using h
  rw [Nat.irreducible_iff_prime, Ideal.absNorm_span_singleton, â† Nat.prime_iff,
    â† Int.prime_iff_natAbs_prime]
  convert Nat.prime_iff_prime_int.1 hp.out
  apply RingHom.injective_int (algebraMap â„¤ â„š)
  rw [â† Algebra.norm_localization (Sâ‚˜ := K) â„¤ (nonZeroDivisors â„¤), Subalgebra.algebraMap_eq]
  simp only [PNat.pow_coe, id.map_eq_id, RingHomCompTriple.comp_eq, RingHom.coe_coe,
    Subalgebra.coe_val, algebraMap_int_eq, map_natCast]
  exact hÎ¶.norm_sub_one_of_prime_ne_two (Polynomial.cyclotomic.irreducible_rat (PNat.pos _)) hodd

/-- `Î¶ - 1` is prime if `Î¶` is a primitive `2 ^ (k + 1)`-th root of unity.
  See `zeta_sub_one_prime` for a general statement. -/
theorem zeta_sub_one_prime_of_two_pow [IsCyclotomicExtension {(2 : â„•+) ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘((2 : â„•+) ^ (k + 1))) :
    Prime (hÎ¶.toInteger - 1) := by
  letI := IsCyclotomicExtension.numberField {(2 : â„•+) ^ (k + 1)} â„š K
  refine Ideal.prime_of_irreducible_absNorm_span (fun h â†¦ ?_) ?_
  Â· apply hÎ¶.pow_ne_one_of_pos_of_lt zero_lt_one (one_lt_pow (by decide) (by simp))
    rw [â† Subalgebra.coe_eq_zero] at h
    simpa [sub_eq_zero] using h
  rw [Nat.irreducible_iff_prime, Ideal.absNorm_span_singleton, â† Nat.prime_iff,
    â† Int.prime_iff_natAbs_prime]
  cases k
  Â· convert Prime.neg Int.prime_two
    apply RingHom.injective_int (algebraMap â„¤ â„š)
    rw [â† Algebra.norm_localization (Sâ‚˜ := K) â„¤ (nonZeroDivisors â„¤), Subalgebra.algebraMap_eq]
    simp only [Nat.zero_eq, PNat.pow_coe, id.map_eq_id, RingHomCompTriple.comp_eq, RingHom.coe_coe,
      Subalgebra.coe_val, algebraMap_int_eq, map_neg, map_ofNat]
    simpa using hÎ¶.norm_pow_sub_one_of_eq_two (cyclotomic.irreducible_rat (by simp))
  convert Int.prime_two
  apply RingHom.injective_int (algebraMap â„¤ â„š)
  rw [â† Algebra.norm_localization (Sâ‚˜ := K) â„¤ (nonZeroDivisors â„¤), Subalgebra.algebraMap_eq]
  simp only [PNat.pow_coe, id.map_eq_id, RingHomCompTriple.comp_eq, RingHom.coe_coe,
    Subalgebra.coe_val, algebraMap_int_eq, map_natCast]
  exact hÎ¶.norm_sub_one_of_eq_two Nat.AtLeastTwo.prop (cyclotomic.irreducible_rat (by simp))

/-- `Î¶ - 1` is prime if `Î¶` is a primitive `p ^ (k + 1)`-th root of unity. -/
theorem zeta_sub_one_prime [IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(p ^ (k + 1))) : Prime (hÎ¶.toInteger - 1) := by
  by_cases htwo : p = 2
  Â· subst htwo
    apply hÎ¶.zeta_sub_one_prime_of_two_pow
  Â· apply hÎ¶.zeta_sub_one_prime_of_ne_two htwo

/-- `Î¶ - 1` is prime if `Î¶` is a primitive `p`-th root of unity. -/
theorem zeta_sub_one_prime' [h : IsCyclotomicExtension {p} â„š K] (hÎ¶ : IsPrimitiveRoot Î¶ p) :
    Prime ((hÎ¶.toInteger - 1)) := by
  convert zeta_sub_one_prime (k := 0) (by simpa)
  simpa

theorem subOneIntegralPowerBasis_gen_prime [IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(p ^ (k + 1))) :
    Prime hÎ¶.subOneIntegralPowerBasis.gen := by simpa using hÎ¶.zeta_sub_one_prime

theorem subOneIntegralPowerBasis'_gen_prime [IsCyclotomicExtension {p} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘p) :
    Prime hÎ¶.subOneIntegralPowerBasis'.gen := by simpa using hÎ¶.zeta_sub_one_prime'

open nonZeroDivisors in
/-- The norm, relative to `â„¤`, of `Î¶ ^ p ^ s - 1` in a `p ^ (k + 1)`-th cyclotomic extension of `â„š`
is p ^ p ^ s` if `s â‰¤ k` and `p ^ (k - s + 1) â‰  2`. -/
lemma norm_toInteger_pow_sub_one_of_prime_pow_ne_two [IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(p ^ (k + 1))) {s : â„•} (hs : s â‰¤ k) (htwo : p ^ (k - s + 1) â‰  2) :
    Algebra.norm â„¤ (hÎ¶.toInteger ^ (p : â„•) ^ s - 1) = p ^ (p : â„•) ^ s := by
  apply (algebraMap â„¤ â„š).injective_int
  have : NumberField K := IsCyclotomicExtension.numberField {p ^ (k + 1)} â„š K
  have : algebraMap (ğ“ K) K (hÎ¶.toInteger ^ (p : â„•) ^ s - 1) = Î¶ ^ (p : â„•) ^ s - 1 := by
    simp only [map_sub, map_pow, map_one, sub_left_inj]
    exact rfl
  rw [â† Algebra.norm_localization (Sâ‚˜ := K) â„¤ â„¤â°, this, hÎ¶.norm_pow_sub_one_of_prime_pow_ne_two
    (cyclotomic.irreducible_rat (by simp)) hs htwo]
  simp

open nonZeroDivisors in
/-- The norm, relative to `â„¤`, of `Î¶ ^ 2 ^ k - 1` in a `2 ^ (k + 1)`-th cyclotomic extension of `â„š`
is `(-2) ^ 2 ^ k`. -/
lemma norm_toInteger_pow_sub_one_of_two [IsCyclotomicExtension {2 ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘((2 : â„•+) ^ (k + 1))) :
    Algebra.norm â„¤ (hÎ¶.toInteger ^ 2 ^ k - 1) = (-2) ^ (2 : â„•) ^ k := by
  apply (algebraMap â„¤ â„š).injective_int
  have : NumberField K := IsCyclotomicExtension.numberField {2 ^ (k + 1)} â„š K
  have : algebraMap (ğ“ K) K (hÎ¶.toInteger ^ 2 ^ k - 1) = Î¶ ^ (2 : â„•) ^ k - 1 := by
    simp only [map_sub, map_pow, map_one, sub_left_inj]
    exact rfl
  rw [â† Algebra.norm_localization (Sâ‚˜ := K) â„¤ â„¤â°, this, hÎ¶.norm_pow_sub_one_of_eq_two
    (cyclotomic.irreducible_rat (pow_pos (by decide) _))]
  simp

/-- The norm, relative to `â„¤`, of `Î¶ ^ p ^ s - 1` in a `p ^ (k + 1)`-th cyclotomic extension of `â„š`
is `p ^ p ^ s` if `s â‰¤ k` and `p â‰  2`. -/
lemma norm_toInteger_pow_sub_one_of_prime_ne_two [IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(p ^ (k + 1))) {s : â„•} (hs : s â‰¤ k) (hodd : p â‰  2) :
    Algebra.norm â„¤ (hÎ¶.toInteger ^ (p : â„•) ^ s - 1) = p ^ (p : â„•) ^ s := by
  have coe_two : ((2 : â„•+) : â„•) = 2 := by norm_cast
  refine hÎ¶.norm_toInteger_pow_sub_one_of_prime_pow_ne_two hs (fun h â†¦ ?_)
  rw [â† PNat.coe_inj, coe_two, PNat.pow_coe, â† pow_one 2] at h
  exact hodd (PNat.coe_injective (by simp [eq_of_prime_pow_eq
    (Nat.prime_iff.1 hp.out) (Nat.prime_iff.1 Nat.prime_two) (k - s).succ_pos h]))

/-- The norm, relative to `â„¤`, of `Î¶ - 1` in a `p ^ (k + 1)`-th cyclotomic extension of `â„š` is
`p` if `p â‰  2`. -/
lemma norm_toInteger_sub_one_of_prime_ne_two [IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(p ^ (k + 1))) (hodd : p â‰  2) :
    Algebra.norm â„¤ (hÎ¶.toInteger - 1) = p := by
  simpa using hÎ¶.norm_toInteger_pow_sub_one_of_prime_ne_two (Nat.zero_le _) hodd

/-- The norm, relative to `â„¤`, of `Î¶ - 1` in a `p`-th cyclotomic extension of `â„š` is `p` if
`p â‰  2`. -/
lemma norm_toInteger_sub_one_of_prime_ne_two' [hcycl : IsCyclotomicExtension {p} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ p) (h : p â‰  2) : Algebra.norm â„¤ (hÎ¶.toInteger - 1) = p := by
  have : IsCyclotomicExtension {p ^ (0 + 1)} â„š K := by simpa using hcycl
  replace hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (0 + 1)) := by simpa using hÎ¶
  exact hÎ¶.norm_toInteger_sub_one_of_prime_ne_two h

/-- The norm, relative to `â„¤`, of `Î¶ - 1` in a `p ^ (k + 1)`-th cyclotomic extension of `â„š` is
a prime if `p ^ (k  + 1) â‰  2`. -/
lemma prime_norm_toInteger_sub_one_of_prime_pow_ne_two [IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(p ^ (k + 1))) (htwo : p ^ (k  + 1) â‰  2) :
    Prime (Algebra.norm â„¤ (hÎ¶.toInteger - 1)) := by
  have := hÎ¶.norm_toInteger_pow_sub_one_of_prime_pow_ne_two (zero_le _) htwo
  simp only [pow_zero, pow_one] at this
  rw [this]
  exact Nat.prime_iff_prime_int.1 hp.out

/-- The norm, relative to `â„¤`, of `Î¶ - 1` in a `p ^ (k + 1)`-th cyclotomic extension of `â„š` is
a prime if `p â‰  2`. -/
lemma prime_norm_toInteger_sub_one_of_prime_ne_two [hcycl : IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(p ^ (k + 1))) (hodd : p â‰  2) :
    Prime (Algebra.norm â„¤ (hÎ¶.toInteger - 1)) := by
  have := hÎ¶.norm_toInteger_sub_one_of_prime_ne_two hodd
  simp only [pow_zero, pow_one] at this
  rw [this]
  exact Nat.prime_iff_prime_int.1 hp.out

/-- The norm, relative to `â„¤`, of `Î¶ - 1` in a `p`-th cyclotomic extension of `â„š` is a prime if
`p â‰  2`. -/
lemma prime_norm_toInteger_sub_one_of_prime_ne_two' [hcycl : IsCyclotomicExtension {p} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ â†‘p) (hodd : p â‰  2) :
    Prime (Algebra.norm â„¤ (hÎ¶.toInteger - 1)) := by
  have : IsCyclotomicExtension {p ^ (0 + 1)} â„š K := by simpa using hcycl
  replace hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (0 + 1)) := by simpa using hÎ¶
  exact hÎ¶.prime_norm_toInteger_sub_one_of_prime_ne_two hodd

end IsPrimitiveRoot

section absdiscr

namespace IsCyclotomicExtension.Rat

open nonZeroDivisors IsPrimitiveRoot

variable (K p k)

/-- We compute the absolute discriminant of a `p ^ k`-th cyclotomic field.
  Beware that in the cases `p ^ k = 1` and `p ^ k = 2` the formula uses `1 / 2 = 0` and `0 - 1 = 0`.
  See also the results below. -/
theorem absdiscr_prime_pow [NumberField K] [IsCyclotomicExtension {p ^ k} â„š K] :
    NumberField.discr K =
    (-1) ^ ((p ^ k : â„•).totient / 2) * p ^ ((p : â„•) ^ (k - 1) * ((p - 1) * k - 1)) := by
  have hÎ¶ := IsCyclotomicExtension.zeta_spec (p ^ k) â„š K
  let pBâ‚ := integralPowerBasis hÎ¶
  apply (algebraMap â„¤ â„š).injective_int
  rw [â† NumberField.discr_eq_discr _ pBâ‚.basis, â† Algebra.discr_localizationLocalization â„¤ â„¤â° K]
  convert IsCyclotomicExtension.discr_prime_pow hÎ¶ (cyclotomic.irreducible_rat (p ^ k).2) using 1
  Â· have : pBâ‚.dim = (IsPrimitiveRoot.powerBasis â„š hÎ¶).dim := by
      rw [â† PowerBasis.finrank, â† PowerBasis.finrank]
      exact RingOfIntegers.rank K
    rw [â† Algebra.discr_reindex _ _ (finCongr this)]
    congr 1
    ext i
    simp_rw [Function.comp_apply, Basis.localizationLocalization_apply, powerBasis_dim,
      PowerBasis.coe_basis, pBâ‚, integralPowerBasis_gen]
    convert â† ((IsPrimitiveRoot.powerBasis â„š hÎ¶).basis_eq_pow i).symm using 1
  Â· simp_rw [algebraMap_int_eq, map_mul, map_pow, map_neg, map_one, map_natCast]

open Nat in
/-- We compute the absolute discriminant of a `p ^ (k + 1)`-th cyclotomic field.
  Beware that in the case `p ^ k = 2` the formula uses `1 / 2 = 0`. See also the results below. -/
theorem absdiscr_prime_pow_succ [NumberField K] [IsCyclotomicExtension {p ^ (k + 1)} â„š K] :
    NumberField.discr K =
    (-1) ^ ((p : â„•) ^ k * (p - 1) / 2) * p ^ ((p : â„•) ^ k * ((p - 1) * (k + 1) - 1)) := by
  simpa [totient_prime_pow hp.out (succ_pos k)] using absdiscr_prime_pow p (k + 1) K

/-- We compute the absolute discriminant of a `p`-th cyclotomic field where `p` is prime. -/
theorem absdiscr_prime [NumberField K] [IsCyclotomicExtension {p} â„š K] :
    NumberField.discr K = (-1) ^ (((p : â„•) - 1) / 2) * p ^ ((p : â„•) - 2) := by
  have : IsCyclotomicExtension {p ^ (0 + 1)} â„š K := by
    rw [zero_add, pow_one]
    infer_instance
  rw [absdiscr_prime_pow_succ p 0 K]
  simp only [Int.reduceNeg, pow_zero, one_mul, zero_add, mul_one, mul_eq_mul_left_iff, gt_iff_lt,
    Nat.cast_pos, PNat.pos, pow_eq_zero_iff', neg_eq_zero, one_ne_zero, ne_eq, false_and, or_false]
  rfl

end IsCyclotomicExtension.Rat

end absdiscr

end PowerBasis
