/-
Copyright (c) 2022 Riccardo Brasca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Riccardo Brasca
-/
import Mathlib.NumberTheory.Cyclotomic.Discriminant
import Mathlib.RingTheory.Ideal.Norm.AbsNorm
import Mathlib.RingTheory.Norm.Transitivity
import Mathlib.RingTheory.Polynomial.Eisenstein.IsIntegral
import Mathlib.RingTheory.Prime

/-!
# Ring of integers of `p ^ n`-th cyclotomic fields
We gather results about cyclotomic extensions of `‚Ñö`. In particular, we compute the ring of
integers of a `p ^ n`-th cyclotomic extension of `‚Ñö`.

## Main results
* `IsCyclotomicExtension.Rat.isIntegralClosure_adjoin_singleton_of_prime_pow`: if `K` is a
  `p ^ k`-th cyclotomic extension of `‚Ñö`, then `(adjoin ‚Ñ§ {Œ∂})` is the integral closure of
  `‚Ñ§` in `K`.
* `IsCyclotomicExtension.Rat.cyclotomicRing_isIntegralClosure_of_prime_pow`: the integral
  closure of `‚Ñ§` inside `CyclotomicField (p ^ k) ‚Ñö` is `CyclotomicRing (p ^ k) ‚Ñ§ ‚Ñö`.
* `IsCyclotomicExtension.Rat.absdiscr_prime_pow` and related results: the absolute discriminant
  of cyclotomic fields.
-/

universe u

open Algebra IsCyclotomicExtension Polynomial NumberField

open scoped Cyclotomic Nat

variable {p : ‚Ñï} {k : ‚Ñï} {K : Type u} [Field K] {Œ∂ : K} [hp : Fact p.Prime]

namespace IsCyclotomicExtension.Rat

variable [CharZero K]

/-- The discriminant of the power basis given by `Œ∂ - 1`. -/
theorem discr_prime_pow_ne_two' [IsCyclotomicExtension {p ^ (k + 1)} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ (k + 1))) (hk : p ^ (k + 1) ‚â† 2) :
    discr ‚Ñö (hŒ∂.subOnePowerBasis ‚Ñö).basis =
      (-1) ^ ((p ^ (k + 1)).totient / 2) * p ^ (p ^ k * ((p - 1) * (k + 1) - 1)) := by
  rw [‚Üê discr_prime_pow_ne_two hŒ∂ (cyclotomic.irreducible_rat (NeZero.pos _)) hk]
  exact hŒ∂.discr_zeta_eq_discr_zeta_sub_one.symm

theorem discr_odd_prime' [IsCyclotomicExtension {p} ‚Ñö K] (hŒ∂ : IsPrimitiveRoot Œ∂ p) (hodd : p ‚â† 2) :
    discr ‚Ñö (hŒ∂.subOnePowerBasis ‚Ñö).basis = (-1) ^ ((p - 1) / 2) * p ^ (p - 2) := by
  rw [‚Üê discr_odd_prime hŒ∂ (cyclotomic.irreducible_rat hp.out.pos) hodd]
  exact hŒ∂.discr_zeta_eq_discr_zeta_sub_one.symm

/-- The discriminant of the power basis given by `Œ∂ - 1`. Beware that in the cases `p ^ k = 1` and
`p ^ k = 2` the formula uses `1 / 2 = 0` and `0 - 1 = 0`. It is useful only to have a uniform
result. See also `IsCyclotomicExtension.Rat.discr_prime_pow_eq_unit_mul_pow'`. -/
theorem discr_prime_pow' [IsCyclotomicExtension {p ^ k} ‚Ñö K] (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ k)) :
    discr ‚Ñö (hŒ∂.subOnePowerBasis ‚Ñö).basis =
      (-1) ^ ((p ^ k).totient / 2) * p ^ (p ^ (k - 1) * ((p - 1) * k - 1)) := by
  rw [‚Üê discr_prime_pow hŒ∂ (cyclotomic.irreducible_rat (NeZero.pos _))]
  exact hŒ∂.discr_zeta_eq_discr_zeta_sub_one.symm

/-- If `p` is a prime and `IsCyclotomicExtension {p ^ k} K L`, then there are `u : ‚Ñ§À£` and
`n : ‚Ñï` such that the discriminant of the power basis given by `Œ∂ - 1` is `u * p ^ n`. Often this is
enough and less cumbersome to use than `IsCyclotomicExtension.Rat.discr_prime_pow'`. -/
theorem discr_prime_pow_eq_unit_mul_pow' [IsCyclotomicExtension {p ^ k} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ k)) :
    ‚àÉ (u : ‚Ñ§À£) (n : ‚Ñï), discr ‚Ñö (hŒ∂.subOnePowerBasis ‚Ñö).basis = u * p ^ n := by
  rw [hŒ∂.discr_zeta_eq_discr_zeta_sub_one.symm]
  exact discr_prime_pow_eq_unit_mul_pow hŒ∂ (cyclotomic.irreducible_rat (NeZero.pos _))

/-- If `K` is a `p ^ k`-th cyclotomic extension of `‚Ñö`, then `(adjoin ‚Ñ§ {Œ∂})` is the
integral closure of `‚Ñ§` in `K`. -/
theorem isIntegralClosure_adjoin_singleton_of_prime_pow [hcycl : IsCyclotomicExtension {p ^ k} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ k)) : IsIntegralClosure (adjoin ‚Ñ§ ({Œ∂} : Set K)) ‚Ñ§ K := by
  refine ‚ü®Subtype.val_injective, @fun x => ‚ü®fun h => ‚ü®‚ü®x, ?_‚ü©, rfl‚ü©, ?_‚ü©‚ü©
  swap
  ¬∑ rintro ‚ü®y, rfl‚ü©
    exact
      IsIntegral.algebraMap
        ((le_integralClosure_iff_isIntegral.1
          (adjoin_le_integralClosure (hŒ∂.isIntegral (NeZero.pos _)))).isIntegral _)
  let B := hŒ∂.subOnePowerBasis ‚Ñö
  have hint : IsIntegral ‚Ñ§ B.gen := (hŒ∂.isIntegral (NeZero.pos _)).sub isIntegral_one
  -- This can't be a `local instance` because it has metavariables.
  letI := IsCyclotomicExtension.finiteDimensional {p ^ k} ‚Ñö K
  have H := discr_mul_isIntegral_mem_adjoin ‚Ñö hint h
  obtain ‚ü®u, n, hun‚ü© := discr_prime_pow_eq_unit_mul_pow' hŒ∂
  rw [hun] at H
  replace H := Subalgebra.smul_mem _ H u.inv
  rw [‚Üê smul_assoc, ‚Üê smul_mul_assoc, Units.inv_eq_val_inv, zsmul_eq_mul, ‚Üê Int.cast_mul,
    Units.inv_mul, Int.cast_one, one_mul, smul_def, map_pow] at H
  cases k
  ¬∑ haveI : IsCyclotomicExtension {1} ‚Ñö K := by simpa using hcycl
    have : x ‚àà (‚ä• : Subalgebra ‚Ñö K) := by
      rw [singleton_one ‚Ñö K]
      exact mem_top
    obtain ‚ü®y, rfl‚ü© := mem_bot.1 this
    replace h := (isIntegral_algebraMap_iff (algebraMap ‚Ñö K).injective).1 h
    obtain ‚ü®z, hz‚ü© := IsIntegrallyClosed.isIntegral_iff.1 h
    rw [‚Üê hz, ‚Üê IsScalarTower.algebraMap_apply]
    exact Subalgebra.algebraMap_mem _ _
  ¬∑ have hmin : (minpoly ‚Ñ§ B.gen).IsEisensteinAt (Submodule.span ‚Ñ§ {(p : ‚Ñ§)}) := by
      have h‚ÇÅ := minpoly.isIntegrallyClosed_eq_field_fractions' ‚Ñö hint
      have h‚ÇÇ := hŒ∂.minpoly_sub_one_eq_cyclotomic_comp (cyclotomic.irreducible_rat (NeZero.pos _))
      rw [IsPrimitiveRoot.subOnePowerBasis_gen] at h‚ÇÅ
      rw [h‚ÇÅ, ‚Üê map_cyclotomic_int, show Int.castRingHom ‚Ñö = algebraMap ‚Ñ§ ‚Ñö by rfl,
        show X + 1 = map (algebraMap ‚Ñ§ ‚Ñö) (X + 1) by simp, ‚Üê map_comp] at h‚ÇÇ
      rw [IsPrimitiveRoot.subOnePowerBasis_gen,
        map_injective (algebraMap ‚Ñ§ ‚Ñö) (algebraMap ‚Ñ§ ‚Ñö).injective_int h‚ÇÇ]
      exact cyclotomic_prime_pow_comp_X_add_one_isEisensteinAt p _
    refine
      adjoin_le ?_
        (mem_adjoin_of_smul_prime_pow_smul_of_minpoly_isEisensteinAt (n := n)
          (Nat.prime_iff_prime_int.1 hp.out) hint h (by simpa using H) hmin)
    simp only [Set.singleton_subset_iff, SetLike.mem_coe]
    exact Subalgebra.sub_mem _ (self_mem_adjoin_singleton ‚Ñ§ _) (Subalgebra.one_mem _)

theorem isIntegralClosure_adjoin_singleton_of_prime [hcycl : IsCyclotomicExtension {p} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ p) : IsIntegralClosure (adjoin ‚Ñ§ ({Œ∂} : Set K)) ‚Ñ§ K := by
  rw [‚Üê pow_one p] at hŒ∂ hcycl
  exact isIntegralClosure_adjoin_singleton_of_prime_pow hŒ∂

/-- The integral closure of `‚Ñ§` inside `CyclotomicField (p ^ k) ‚Ñö` is
`CyclotomicRing (p ^ k) ‚Ñ§ ‚Ñö`. -/
theorem cyclotomicRing_isIntegralClosure_of_prime_pow :
    IsIntegralClosure (CyclotomicRing (p ^ k) ‚Ñ§ ‚Ñö) ‚Ñ§ (CyclotomicField (p ^ k) ‚Ñö) := by
  have hŒ∂ := zeta_spec (p ^ k) ‚Ñö (CyclotomicField (p ^ k) ‚Ñö)
  refine ‚ü®IsFractionRing.injective _ _, @fun x => ‚ü®fun h => ‚ü®‚ü®x, ?_‚ü©, rfl‚ü©, ?_‚ü©‚ü©
  ¬∑ obtain ‚ü®y, rfl‚ü© := (isIntegralClosure_adjoin_singleton_of_prime_pow hŒ∂).isIntegral_iff.1 h
    refine adjoin_mono ?_ y.2
    simp only [Set.singleton_subset_iff, Set.mem_setOf_eq]
    exact hŒ∂.pow_eq_one
  ¬∑ rintro ‚ü®y, rfl‚ü©
    exact IsIntegral.algebraMap ((IsCyclotomicExtension.integral {p ^ k} ‚Ñ§ _).isIntegral _)

theorem cyclotomicRing_isIntegralClosure_of_prime :
    IsIntegralClosure (CyclotomicRing p ‚Ñ§ ‚Ñö) ‚Ñ§ (CyclotomicField p ‚Ñö) := by
  rw [‚Üê pow_one p]
  exact cyclotomicRing_isIntegralClosure_of_prime_pow

end IsCyclotomicExtension.Rat

section PowerBasis

open IsCyclotomicExtension.Rat

namespace IsPrimitiveRoot

section CharZero

variable [CharZero K]

/-- The algebra isomorphism `adjoin ‚Ñ§ {Œ∂} ‚âÉ‚Çê[‚Ñ§] (ùìû K)`, where `Œ∂` is a primitive `p ^ k`-th root of
unity and `K` is a `p ^ k`-th cyclotomic extension of `‚Ñö`. -/
@[simps!]
noncomputable def _root_.IsPrimitiveRoot.adjoinEquivRingOfIntegers
    [IsCyclotomicExtension {p ^ k} ‚Ñö K] (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ k)) :
    adjoin ‚Ñ§ ({Œ∂} : Set K) ‚âÉ‚Çê[‚Ñ§] ùìû K :=
  let _ := isIntegralClosure_adjoin_singleton_of_prime_pow hŒ∂
  IsIntegralClosure.equiv ‚Ñ§ (adjoin ‚Ñ§ ({Œ∂} : Set K)) K (ùìû K)

/-- The ring of integers of a `p ^ k`-th cyclotomic extension of `‚Ñö` is a cyclotomic extension. -/
instance IsCyclotomicExtension.ringOfIntegers [IsCyclotomicExtension {p ^ k} ‚Ñö K] :
    IsCyclotomicExtension {p ^ k} ‚Ñ§ (ùìû K) :=
  let _ := (zeta_spec (p ^ k) ‚Ñö K).adjoin_isCyclotomicExtension ‚Ñ§
  IsCyclotomicExtension.equiv _ ‚Ñ§ _ (zeta_spec (p ^ k) ‚Ñö K).adjoinEquivRingOfIntegers

/-- The integral `PowerBasis` of `ùìû K` given by a primitive root of unity, where `K` is a `p ^ k`
cyclotomic extension of `‚Ñö`. -/
noncomputable def integralPowerBasis [IsCyclotomicExtension {p ^ k} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ k)) : PowerBasis ‚Ñ§ (ùìû K) :=
  (Algebra.adjoin.powerBasis' (hŒ∂.isIntegral (NeZero.pos _))).map hŒ∂.adjoinEquivRingOfIntegers

/-- Abbreviation to see a primitive root of unity as a member of the ring of integers. -/
abbrev toInteger {k : ‚Ñï} [NeZero k] (hŒ∂ : IsPrimitiveRoot Œ∂ k) : ùìû K :=
  ‚ü®Œ∂, hŒ∂.isIntegral (NeZero.pos _)‚ü©

end CharZero

lemma coe_toInteger {k : ‚Ñï} [NeZero k] (hŒ∂ : IsPrimitiveRoot Œ∂ k) : hŒ∂.toInteger.1 = Œ∂ := rfl

/-- `ùìû K ‚ß∏ Ideal.span {Œ∂ - 1}` is finite. -/
lemma finite_quotient_toInteger_sub_one [NumberField K] {k : ‚Ñï} (hk : 1 < k)
    (hŒ∂ : IsPrimitiveRoot Œ∂ k) :
    haveI : NeZero k := NeZero.of_gt hk
    Finite (ùìû K ‚ß∏ Ideal.span {hŒ∂.toInteger - 1}) := by
  refine Ideal.finiteQuotientOfFreeOfNeBot _ (fun h ‚Ü¶ ?_)
  simp only [Ideal.span_singleton_eq_bot, sub_eq_zero] at h
  exact hŒ∂.ne_one hk (RingOfIntegers.ext_iff.1 h)

/-- We have that `ùìû K ‚ß∏ Ideal.span {Œ∂ - 1}` has cardinality equal to the norm of `Œ∂ - 1`.

See the results below to compute this norm in various cases. -/
lemma card_quotient_toInteger_sub_one [NumberField K] {k : ‚Ñï} [NeZero k]
    (hŒ∂ : IsPrimitiveRoot Œ∂ k) :
    Nat.card (ùìû K ‚ß∏ Ideal.span {hŒ∂.toInteger - 1}) =
      (Algebra.norm ‚Ñ§ (hŒ∂.toInteger - 1)).natAbs := by
  rw [‚Üê Submodule.cardQuot_apply, ‚Üê Ideal.absNorm_apply, Ideal.absNorm_span_singleton]

lemma toInteger_isPrimitiveRoot {k : ‚Ñï} [NeZero k] (hŒ∂ : IsPrimitiveRoot Œ∂ k) :
    IsPrimitiveRoot hŒ∂.toInteger k :=
  IsPrimitiveRoot.of_map_of_injective (by exact hŒ∂) RingOfIntegers.coe_injective

variable [CharZero K]

@[simp]
theorem integralPowerBasis_gen [hcycl : IsCyclotomicExtension {p ^ k} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ k)) :
    hŒ∂.integralPowerBasis.gen = hŒ∂.toInteger :=
  Subtype.ext <| show algebraMap _ K hŒ∂.integralPowerBasis.gen = _ by
    rw [integralPowerBasis, PowerBasis.map_gen, adjoin.powerBasis'_gen]
    simp only [adjoinEquivRingOfIntegers_apply, IsIntegralClosure.algebraMap_lift]
    rfl

/- We name `hcycl` so it can be used as a named argument, but this is unused in the declaration
otherwise, so we need to disable the linter. -/
set_option linter.unusedVariables false in
@[simp]
theorem integralPowerBasis_dim [hcycl : IsCyclotomicExtension {p ^ k} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ k)) : hŒ∂.integralPowerBasis.dim = œÜ (p ^ k) := by
  simp [integralPowerBasis, ‚Üê cyclotomic_eq_minpoly hŒ∂ (NeZero.pos _), natDegree_cyclotomic]

/-- The algebra isomorphism `adjoin ‚Ñ§ {Œ∂} ‚âÉ‚Çê[‚Ñ§] (ùìû K)`, where `Œ∂` is a primitive `p`-th root of
unity and `K` is a `p`-th cyclotomic extension of `‚Ñö`. -/
@[simps!]
noncomputable def _root_.IsPrimitiveRoot.adjoinEquivRingOfIntegers'
    [hcycl : IsCyclotomicExtension {p} ‚Ñö K] (hŒ∂ : IsPrimitiveRoot Œ∂ p) :
    adjoin ‚Ñ§ ({Œ∂} : Set K) ‚âÉ‚Çê[‚Ñ§] ùìû K :=
  have : IsCyclotomicExtension {p ^ 1} ‚Ñö K := by convert hcycl; rw [pow_one]
  adjoinEquivRingOfIntegers (p := p) (k := 1) (Œ∂ := Œ∂) (by rwa [pow_one])

/-- The ring of integers of a `p`-th cyclotomic extension of `‚Ñö` is a cyclotomic extension. -/
instance _root_.IsCyclotomicExtension.ring_of_integers' [IsCyclotomicExtension {p} ‚Ñö K] :
    IsCyclotomicExtension {p} ‚Ñ§ (ùìû K) :=
  let _ := (zeta_spec p ‚Ñö K).adjoin_isCyclotomicExtension ‚Ñ§
  IsCyclotomicExtension.equiv _ ‚Ñ§ _ (zeta_spec p ‚Ñö K).adjoinEquivRingOfIntegers'

/-- The integral `PowerBasis` of `ùìû K` given by a primitive root of unity, where `K` is a `p`-th
cyclotomic extension of `‚Ñö`. -/
noncomputable def integralPowerBasis' [hcycl : IsCyclotomicExtension {p} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ p) : PowerBasis ‚Ñ§ (ùìû K) :=
  have : IsCyclotomicExtension {p ^ 1} ‚Ñö K := by convert hcycl; rw [pow_one]
  integralPowerBasis (p := p) (k := 1) (Œ∂ := Œ∂) (by rwa [pow_one])

@[simp]
theorem integralPowerBasis'_gen [hcycl : IsCyclotomicExtension {p} ‚Ñö K] (hŒ∂ : IsPrimitiveRoot Œ∂ p) :
    hŒ∂.integralPowerBasis'.gen = hŒ∂.toInteger :=
  integralPowerBasis_gen (hcycl := by rwa [pow_one]) (by rwa [pow_one])

@[simp]
theorem power_basis_int'_dim [hcycl : IsCyclotomicExtension {p} ‚Ñö K] (hŒ∂ : IsPrimitiveRoot Œ∂ p) :
    hŒ∂.integralPowerBasis'.dim = œÜ p := by
  rw [integralPowerBasis', integralPowerBasis_dim (hcycl := by rwa [pow_one]) (by rwa [pow_one]),
    pow_one]


/-- The integral `PowerBasis` of `ùìû K` given by `Œ∂ - 1`, where `K` is a `p ^ k` cyclotomic
extension of `‚Ñö`. -/
noncomputable def subOneIntegralPowerBasis [IsCyclotomicExtension {p ^ k} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ k)) : PowerBasis ‚Ñ§ (ùìû K) :=
  PowerBasis.ofGenMemAdjoin' hŒ∂.integralPowerBasis (RingOfIntegers.isIntegral _)
    (by
      simp only [integralPowerBasis_gen, toInteger]
      convert Subalgebra.add_mem _ (self_mem_adjoin_singleton ‚Ñ§ (‚ü®Œ∂ - 1, _‚ü© : ùìû K))
        (Subalgebra.one_mem _)
      ¬∑ simp
      ¬∑ exact Subalgebra.sub_mem _ (hŒ∂.isIntegral (NeZero.pos _)) (Subalgebra.one_mem _))

@[simp]
theorem subOneIntegralPowerBasis_gen [IsCyclotomicExtension {p ^ k} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ k)) :
    hŒ∂.subOneIntegralPowerBasis.gen =
      ‚ü®Œ∂ - 1, Subalgebra.sub_mem _ (hŒ∂.isIntegral (NeZero.pos _)) (Subalgebra.one_mem _)‚ü© := by
  simp [subOneIntegralPowerBasis]

/-- The integral `PowerBasis` of `ùìû K` given by `Œ∂ - 1`, where `K` is a `p`-th cyclotomic
extension of `‚Ñö`. -/
noncomputable def subOneIntegralPowerBasis' [IsCyclotomicExtension {p} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ p) : PowerBasis ‚Ñ§ (ùìû K) :=
  have : IsCyclotomicExtension {p ^ 1} ‚Ñö K := by rwa [pow_one]
  subOneIntegralPowerBasis (p := p) (k := 1) (Œ∂ := Œ∂) (by rwa [pow_one])

@[simp, nolint unusedHavesSuffices]
theorem subOneIntegralPowerBasis'_gen [IsCyclotomicExtension {p} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ p) :
    hŒ∂.subOneIntegralPowerBasis'.gen = hŒ∂.toInteger - 1 :=
  -- The `unusedHavesSuffices` linter incorrectly thinks this `have` is unnecessary.
  have : IsCyclotomicExtension {p ^ 1} ‚Ñö K := by rwa [pow_one]
  subOneIntegralPowerBasis_gen (by rwa [pow_one])

/-- `Œ∂ - 1` is prime if `p ‚â† 2` and `Œ∂` is a primitive `p ^ (k + 1)`-th root of unity.
  See `zeta_sub_one_prime` for a general statement. -/
theorem zeta_sub_one_prime_of_ne_two [IsCyclotomicExtension {p ^ (k + 1)} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ (k + 1))) (hodd : p ‚â† 2) :
    Prime (hŒ∂.toInteger - 1) := by
  letI := IsCyclotomicExtension.numberField {p ^ (k + 1)} ‚Ñö K
  refine Ideal.prime_of_irreducible_absNorm_span (fun h ‚Ü¶ ?_) ?_
  ¬∑ apply hŒ∂.pow_ne_one_of_pos_of_lt zero_lt_one (one_lt_pow‚ÇÄ hp.out.one_lt (by simp))
    rw [sub_eq_zero] at h
    simpa using congrArg (algebraMap _ K) h
  rw [Nat.irreducible_iff_prime, Ideal.absNorm_span_singleton, ‚Üê Nat.prime_iff,
    ‚Üê Int.prime_iff_natAbs_prime]
  convert Nat.prime_iff_prime_int.1 hp.out
  apply RingHom.injective_int (algebraMap ‚Ñ§ ‚Ñö)
  rw [‚Üê Algebra.norm_localization (S‚Çò := K) ‚Ñ§ (nonZeroDivisors ‚Ñ§)]
  simp only [algebraMap_int_eq, map_natCast]
  exact hŒ∂.norm_sub_one_of_prime_ne_two (Polynomial.cyclotomic.irreducible_rat (NeZero.pos _)) hodd

/-- `Œ∂ - 1` is prime if `Œ∂` is a primitive `2 ^ (k + 1)`-th root of unity.
  See `zeta_sub_one_prime` for a general statement. -/
theorem zeta_sub_one_prime_of_two_pow [IsCyclotomicExtension {2 ^ (k + 1)} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (2 ^ (k + 1))) :
    Prime (hŒ∂.toInteger - 1) := by
  have := IsCyclotomicExtension.numberField {2 ^ (k + 1)} ‚Ñö K
  refine Ideal.prime_of_irreducible_absNorm_span (fun h ‚Ü¶ ?_) ?_
  ¬∑ apply hŒ∂.pow_ne_one_of_pos_of_lt zero_lt_one (one_lt_pow‚ÇÄ (by decide) (by simp))
    rw [sub_eq_zero] at h
    simpa using congrArg (algebraMap _ K) h
  rw [Nat.irreducible_iff_prime, Ideal.absNorm_span_singleton, ‚Üê Nat.prime_iff,
    ‚Üê Int.prime_iff_natAbs_prime]
  cases k
  ¬∑ convert Prime.neg Int.prime_two
    apply RingHom.injective_int (algebraMap ‚Ñ§ ‚Ñö)
    rw [‚Üê Algebra.norm_localization (S‚Çò := K) ‚Ñ§ (nonZeroDivisors ‚Ñ§)]
    simp only [algebraMap_int_eq, map_neg, map_ofNat]
    simpa only [zero_add, pow_one, AddSubgroupClass.coe_sub, OneMemClass.coe_one,
        pow_zero]
      using hŒ∂.norm_pow_sub_one_two (cyclotomic.irreducible_rat
        (by simp only [zero_add, pow_one, Nat.ofNat_pos]))
  convert Int.prime_two
  apply RingHom.injective_int (algebraMap ‚Ñ§ ‚Ñö)
  rw [‚Üê Algebra.norm_localization (S‚Çò := K) ‚Ñ§ (nonZeroDivisors ‚Ñ§), algebraMap_int_eq]
  exact hŒ∂.norm_sub_one_two Nat.AtLeastTwo.prop (cyclotomic.irreducible_rat (by simp))

/-- `Œ∂ - 1` is prime if `Œ∂` is a primitive `p ^ (k + 1)`-th root of unity. -/
theorem zeta_sub_one_prime [IsCyclotomicExtension {p ^ (k + 1)} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ (k + 1))) : Prime (hŒ∂.toInteger - 1) := by
  by_cases htwo : p = 2
  ¬∑ subst htwo
    apply hŒ∂.zeta_sub_one_prime_of_two_pow
  ¬∑ apply hŒ∂.zeta_sub_one_prime_of_ne_two htwo

/-- `Œ∂ - 1` is prime if `Œ∂` is a primitive `p`-th root of unity. -/
theorem zeta_sub_one_prime' [h : IsCyclotomicExtension {p} ‚Ñö K] (hŒ∂ : IsPrimitiveRoot Œ∂ p) :
    Prime ((hŒ∂.toInteger - 1)) := by
  convert zeta_sub_one_prime (k := 0) (by simpa only [zero_add, pow_one])
  simpa only [zero_add, pow_one]

theorem subOneIntegralPowerBasis_gen_prime [IsCyclotomicExtension {p ^ (k + 1)} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ (k + 1))) :
    Prime hŒ∂.subOneIntegralPowerBasis.gen := by
  simpa only [subOneIntegralPowerBasis_gen] using hŒ∂.zeta_sub_one_prime

theorem subOneIntegralPowerBasis'_gen_prime [IsCyclotomicExtension {p} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ p) :
    Prime hŒ∂.subOneIntegralPowerBasis'.gen := by
  simpa only [subOneIntegralPowerBasis'_gen] using hŒ∂.zeta_sub_one_prime'

/--
The norm, relative to `‚Ñ§`, of `Œ∂ - 1` in a `n`-th cyclotomic extension of `‚Ñö` where `n` is not a
power of a prime number is `1`.
-/
theorem norm_toInteger_sub_one_eq_one {n : ‚Ñï} [IsCyclotomicExtension {n} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ n) (h‚ÇÅ : 2 < n) (h‚ÇÇ : ‚àÄ {p : ‚Ñï}, Nat.Prime p ‚Üí ‚àÄ (k : ‚Ñï), p ^ k ‚â† n) :
    have : NeZero n := NeZero.of_gt h‚ÇÅ
    norm ‚Ñ§ (hŒ∂.toInteger - 1) = 1 := by
  have : NumberField K := IsCyclotomicExtension.numberField {n} ‚Ñö K
  have : NeZero n := NeZero.of_gt h‚ÇÅ
  dsimp only
  rw [norm_eq_iff ‚Ñ§ (S‚Çò := K) (R‚Çò := ‚Ñö) le_rfl, map_sub, map_one, map_one, RingOfIntegers.map_mk,
    sub_one_norm_eq_eval_cyclotomic hŒ∂ h‚ÇÅ (cyclotomic.irreducible_rat (NeZero.pos _)),
    eval_one_cyclotomic_not_prime_pow h‚ÇÇ, Int.cast_one]

/-- The norm, relative to `‚Ñ§`, of `Œ∂ ^ p ^ s - 1` in a `p ^ (k + 1)`-th cyclotomic extension of `‚Ñö`
is p ^ p ^ s` if `s ‚â§ k` and `p ^ (k - s + 1) ‚â† 2`. -/
lemma norm_toInteger_pow_sub_one_of_prime_pow_ne_two [IsCyclotomicExtension {p ^ (k + 1)} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ (k + 1))) {s : ‚Ñï} (hs : s ‚â§ k) (htwo : p ^ (k - s + 1) ‚â† 2) :
    Algebra.norm ‚Ñ§ (hŒ∂.toInteger ^ p ^ s - 1) = p ^ p ^ s := by
  have : NumberField K := IsCyclotomicExtension.numberField {p ^ (k + 1)} ‚Ñö K
  rw [Algebra.norm_eq_iff ‚Ñ§ (S‚Çò := K) (R‚Çò := ‚Ñö) le_rfl]
  simp [hŒ∂.norm_pow_sub_one_of_prime_pow_ne_two (cyclotomic.irreducible_rat (NeZero.pos _)) hs htwo]

/-- The norm, relative to `‚Ñ§`, of `Œ∂ ^ 2 ^ k - 1` in a `2 ^ (k + 1)`-th cyclotomic extension of `‚Ñö`
is `(-2) ^ 2 ^ k`. -/
lemma norm_toInteger_pow_sub_one_of_two [IsCyclotomicExtension {2 ^ (k + 1)} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (2 ^ (k + 1))) :
    Algebra.norm ‚Ñ§ (hŒ∂.toInteger ^ 2 ^ k - 1) = (-2) ^ 2 ^ k := by
  have : NumberField K := IsCyclotomicExtension.numberField {2 ^ (k + 1)} ‚Ñö K
  rw [Algebra.norm_eq_iff ‚Ñ§ (S‚Çò := K) (R‚Çò := ‚Ñö) le_rfl]
  simp [hŒ∂.norm_pow_sub_one_two (cyclotomic.irreducible_rat (pow_pos (by decide) _))]

/-- The norm, relative to `‚Ñ§`, of `Œ∂ ^ p ^ s - 1` in a `p ^ (k + 1)`-th cyclotomic extension of `‚Ñö`
is `p ^ p ^ s` if `s ‚â§ k` and `p ‚â† 2`. -/
lemma norm_toInteger_pow_sub_one_of_prime_ne_two [IsCyclotomicExtension {p ^ (k + 1)} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ (k + 1))) {s : ‚Ñï} (hs : s ‚â§ k) (hodd : p ‚â† 2) :
    Algebra.norm ‚Ñ§ (hŒ∂.toInteger ^ p ^ s - 1) = p ^ p ^ s := by
  refine hŒ∂.norm_toInteger_pow_sub_one_of_prime_pow_ne_two hs (fun h ‚Ü¶ hodd ?_)
  apply eq_of_prime_pow_eq hp.out.prime Nat.prime_two.prime (k - s).succ_pos
  rwa [pow_one]

/--
The norm, relative to `‚Ñ§`, of `Œ∂ - 1` in a `2 ^ (k + 2)`-th cyclotomic extension of `‚Ñö` is `2`.
-/
theorem norm_toInteger_sub_one_of_eq_two_pow {k : ‚Ñï} {K : Type*} [Field K]
    {Œ∂ : K} [CharZero K] [IsCyclotomicExtension {2 ^ (k + 2)} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (2 ^ (k + 2))) :
    norm ‚Ñ§ (hŒ∂.toInteger - 1) = 2 := by
  have : NumberField K := IsCyclotomicExtension.numberField {2 ^ (k + 2)} ‚Ñö K
  rw [norm_eq_iff ‚Ñ§ (S‚Çò := K) (R‚Çò := ‚Ñö) le_rfl, map_sub, map_one, eq_intCast, Int.cast_ofNat,
    RingOfIntegers.map_mk, hŒ∂.norm_sub_one_two (Nat.le_add_left 2 k)
    (Polynomial.cyclotomic.irreducible_rat (Nat.two_pow_pos _))]

/-- The norm, relative to `‚Ñ§`, of `Œ∂ - 1` in a `p ^ (k + 1)`-th cyclotomic extension of `‚Ñö` is
`p` if `p ‚â† 2`. -/
lemma norm_toInteger_sub_one_of_prime_ne_two [IsCyclotomicExtension {p ^ (k + 1)} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ (k + 1))) (hodd : p ‚â† 2) :
    Algebra.norm ‚Ñ§ (hŒ∂.toInteger - 1) = p := by
  simpa only [pow_zero, pow_one] using
    hŒ∂.norm_toInteger_pow_sub_one_of_prime_ne_two (Nat.zero_le _) hodd

/--
The norm, relative to `‚Ñ§`, of `Œ∂ - 1` in a `2`-th cyclotomic extension of `‚Ñö` is `-2`.
-/
theorem norm_toInteger_sub_one_of_eq_two [IsCyclotomicExtension {2} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ 2) :
    norm ‚Ñ§ (hŒ∂.toInteger - 1) = -2 := by
  rw [show 2 = (2 ^ (0 + 1)) by simp] at hŒ∂
  simpa using hŒ∂.norm_toInteger_pow_sub_one_of_two

/-- The norm, relative to `‚Ñ§`, of `Œ∂ - 1` in a `p`-th cyclotomic extension of `‚Ñö` is `p` if
`p ‚â† 2`. -/
lemma norm_toInteger_sub_one_of_prime_ne_two' [hcycl : IsCyclotomicExtension {p} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ p) (h : p ‚â† 2) : Algebra.norm ‚Ñ§ (hŒ∂.toInteger - 1) = p := by
  have : IsCyclotomicExtension {p ^ (0 + 1)} ‚Ñö K := by simpa using hcycl
  replace hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ (0 + 1)) := by simpa using hŒ∂
  exact hŒ∂.norm_toInteger_sub_one_of_prime_ne_two h

/-- The norm, relative to `‚Ñ§`, of `Œ∂ - 1` in a `p ^ (k + 1)`-th cyclotomic extension of `‚Ñö` is
a prime if `p ^ (k  + 1) ‚â† 2`. -/
lemma prime_norm_toInteger_sub_one_of_prime_pow_ne_two [IsCyclotomicExtension {p ^ (k + 1)} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ (k + 1))) (htwo : p ^ (k + 1) ‚â† 2) :
    Prime (Algebra.norm ‚Ñ§ (hŒ∂.toInteger - 1)) := by
  have := hŒ∂.norm_toInteger_pow_sub_one_of_prime_pow_ne_two (zero_le _) htwo
  simp only [pow_zero, pow_one] at this
  rw [this]
  exact Nat.prime_iff_prime_int.1 hp.out

/-- The norm, relative to `‚Ñ§`, of `Œ∂ - 1` in a `p ^ (k + 1)`-th cyclotomic extension of `‚Ñö` is
a prime if `p ‚â† 2`. -/
lemma prime_norm_toInteger_sub_one_of_prime_ne_two [hcycl : IsCyclotomicExtension {p ^ (k + 1)} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ (k + 1))) (hodd : p ‚â† 2) :
    Prime (Algebra.norm ‚Ñ§ (hŒ∂.toInteger - 1)) := by
  have := hŒ∂.norm_toInteger_sub_one_of_prime_ne_two hodd
  simp only at this
  rw [this]
  exact Nat.prime_iff_prime_int.1 hp.out

/-- The norm, relative to `‚Ñ§`, of `Œ∂ - 1` in a `p`-th cyclotomic extension of `‚Ñö` is a prime if
`p ‚â† 2`. -/
lemma prime_norm_toInteger_sub_one_of_prime_ne_two' [hcycl : IsCyclotomicExtension {p} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ p) (hodd : p ‚â† 2) :
    Prime (Algebra.norm ‚Ñ§ (hŒ∂.toInteger - 1)) := by
  have : IsCyclotomicExtension {p ^ (0 + 1)} ‚Ñö K := by simpa using hcycl
  replace hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ (0 + 1)) := by simpa using hŒ∂
  exact hŒ∂.prime_norm_toInteger_sub_one_of_prime_ne_two hodd

/-- In a `p ^ (k + 1)`-th cyclotomic extension of `‚Ñö `, we have that `Œ∂` is not congruent to an
  integer modulo `p` if `p ^ (k  + 1) ‚â† 2`. -/
theorem not_exists_int_prime_dvd_sub_of_prime_pow_ne_two
    [hcycl : IsCyclotomicExtension {p ^ (k + 1)} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ (k + 1))) (htwo : p ^ (k + 1) ‚â† 2) :
    ¬¨(‚àÉ n : ‚Ñ§, (p : ùìû K) ‚à£ (hŒ∂.toInteger - n : ùìû K)) := by
  intro ‚ü®n, x, h‚ü©
  -- Let `pB` be the power basis of `ùìû K` given by powers of `Œ∂`.
  let pB := hŒ∂.integralPowerBasis
  have hdim : pB.dim = p ^ k * (‚Üëp - 1) := by
    simp [integralPowerBasis_dim, pB, Nat.totient_prime_pow hp.1 (Nat.zero_lt_succ k)]
  replace hdim : 1 < pB.dim := by
    rw [Nat.one_lt_iff_ne_zero_and_ne_one, hdim]
    refine ‚ü®by simp only [ne_eq, mul_eq_zero, NeZero.ne _, Nat.sub_eq_zero_iff_le, false_or,
      not_le, Nat.Prime.one_lt hp.out], ne_of_gt ?_‚ü©
    by_cases hk : k = 0
    ¬∑ simp only [hk, zero_add, pow_one, pow_zero, one_mul, Nat.lt_sub_iff_add_lt,
        Nat.reduceAdd] at htwo ‚ä¢
      exact htwo.symm.lt_of_le hp.1.two_le
    ¬∑ exact one_lt_mul_of_lt_of_le (one_lt_pow‚ÇÄ hp.1.one_lt hk)
        (have := Nat.Prime.two_le hp.out; by cutsat)
  rw [sub_eq_iff_eq_add] at h
  -- We are assuming that `Œ∂ = n + p * x` for some integer `n` and `x : ùìû K`. Looking at the
  -- coordinates in the base `pB`, we obtain that `1` is a multiple of `p`, contradiction.
  replace h := pB.basis.ext_elem_iff.1 h ‚ü®1, hdim‚ü©
  have := pB.basis_eq_pow ‚ü®1, hdim‚ü©
  rw [hŒ∂.integralPowerBasis_gen] at this
  simp only [PowerBasis.coe_basis, pow_one] at this
  rw [‚Üê this, show pB.gen = pB.gen ^ (‚ü®1, hdim‚ü© : Fin pB.dim).1 by simp, ‚Üê pB.basis_eq_pow,
    pB.basis.repr_self_apply] at h
  simp only [‚ÜìreduceIte, map_add, Finsupp.coe_add, Pi.add_apply] at h
  rw [show (p : ùìû K) * x = (p : ‚Ñ§) ‚Ä¢ x by simp, ‚Üê pB.basis.coord_apply,
    LinearMap.map_smul, ‚Üê zsmul_one, ‚Üê pB.basis.coord_apply, LinearMap.map_smul,
    show 1 = pB.gen ^ (‚ü®0, by cutsat‚ü© : Fin pB.dim).1 by simp, ‚Üê pB.basis_eq_pow,
    pB.basis.coord_apply, pB.basis.coord_apply, pB.basis.repr_self_apply] at h
  simp only [smul_eq_mul, Fin.mk.injEq, zero_ne_one, ‚ÜìreduceIte, mul_zero, add_zero] at h
  exact (Int.prime_iff_natAbs_prime.2 (by simp [hp.1])).not_dvd_one ‚ü®_, h‚ü©

/-- In a `p ^ (k + 1)`-th cyclotomic extension of `‚Ñö `, we have that `Œ∂` is not congruent to an
  integer modulo `p` if `p ‚â† 2`. -/
theorem not_exists_int_prime_dvd_sub_of_prime_ne_two
    [hcycl : IsCyclotomicExtension {p ^ (k + 1)} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ (k + 1))) (hodd : p ‚â† 2) :
    ¬¨(‚àÉ n : ‚Ñ§, (p : ùìû K) ‚à£ (hŒ∂.toInteger - n : ùìû K)) := by
  refine not_exists_int_prime_dvd_sub_of_prime_pow_ne_two hŒ∂ (fun h ‚Ü¶ ?_)
  simp_all only [(@Nat.Prime.pow_eq_iff 2 p (k + 1) Nat.prime_two).mp (by assumption_mod_cast),
    pow_one, ne_eq]

/-- In a `p`-th cyclotomic extension of `‚Ñö `, we have that `Œ∂` is not congruent to an
  integer modulo `p` if `p ‚â† 2`. -/
theorem not_exists_int_prime_dvd_sub_of_prime_ne_two'
    [hcycl : IsCyclotomicExtension {p} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ p) (hodd : p ‚â† 2) :
    ¬¨(‚àÉ n : ‚Ñ§, (p : ùìû K) ‚à£ (hŒ∂.toInteger - n : ùìû K)) := by
  have : IsCyclotomicExtension {p ^ (0 + 1)} ‚Ñö K := by simpa using hcycl
  replace hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ (0 + 1)) := by simpa using hŒ∂
  exact not_exists_int_prime_dvd_sub_of_prime_ne_two hŒ∂ hodd

theorem finite_quotient_span_sub_one [hcycl : IsCyclotomicExtension {p ^ (k + 1)} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ (k + 1))) :
    Finite (ùìû K ‚ß∏ Ideal.span {hŒ∂.toInteger - 1}) := by
  have : NumberField K := IsCyclotomicExtension.numberField {p ^ (k + 1)} ‚Ñö K
  refine Ideal.finiteQuotientOfFreeOfNeBot _ (fun h ‚Ü¶ ?_)
  simp only [Ideal.span_singleton_eq_bot, sub_eq_zero] at h
  exact hŒ∂.ne_one (one_lt_pow‚ÇÄ hp.1.one_lt (Nat.zero_ne_add_one k).symm)
    (RingOfIntegers.ext_iff.1 h)

theorem finite_quotient_span_sub_one' [hcycl : IsCyclotomicExtension {p} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ p) :
    Finite (ùìû K ‚ß∏ Ideal.span {hŒ∂.toInteger - 1}) := by
  have : IsCyclotomicExtension {p ^ (0 + 1)} ‚Ñö K := by simpa using hcycl
  replace hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ (0 + 1)) := by simpa using hŒ∂
  exact hŒ∂.finite_quotient_span_sub_one

/-- In a `p ^ (k + 1)`-th cyclotomic extension of `‚Ñö`, we have that
  `Œ∂ - 1` divides `p` in `ùìû K`. -/
lemma toInteger_sub_one_dvd_prime [hcycl : IsCyclotomicExtension {p ^ (k + 1)} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ (k + 1))) : ((hŒ∂.toInteger - 1)) ‚à£ p := by
  by_cases htwo : p ^ (k + 1) = 2
  ¬∑ have ‚ü®hp2, hk‚ü© := (Nat.Prime.pow_eq_iff Nat.prime_two).1 htwo
    simp only [add_eq_right] at hk
    have hŒ∂' : Œ∂ = -1 := by
      refine IsPrimitiveRoot.eq_neg_one_of_two_right ?_
      rwa [hk, zero_add, pow_one, hp2] at hŒ∂
    replace hŒ∂' : hŒ∂.toInteger = -1 := by
      ext
      exact hŒ∂'
    rw [hŒ∂', hp2]
    exact ‚ü®-1, by ring‚ü©
  suffices (hŒ∂.toInteger - 1) ‚à£ (p : ‚Ñ§) by simpa
  have := IsCyclotomicExtension.numberField {p ^ (k + 1)} ‚Ñö K
  have H := hŒ∂.norm_toInteger_pow_sub_one_of_prime_pow_ne_two (zero_le _) htwo
  rw [pow_zero, pow_one] at H
  rw [‚Üê Ideal.norm_dvd_iff, H]
  ¬∑ simp
  ¬∑ exact prime_norm_toInteger_sub_one_of_prime_pow_ne_two hŒ∂ htwo

/-- In a `p`-th cyclotomic extension of `‚Ñö`, we have that `Œ∂ - 1` divides `p` in `ùìû K`. -/
lemma toInteger_sub_one_dvd_prime' [hcycl : IsCyclotomicExtension {p} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ p) : hŒ∂.toInteger - 1 ‚à£ p := by
  have : IsCyclotomicExtension {p ^ (0 + 1)} ‚Ñö K := by simpa using hcycl
  replace hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ (0 + 1)) := by simpa using hŒ∂
  exact toInteger_sub_one_dvd_prime hŒ∂

/-- We have that `hŒ∂.toInteger - 1` does not divide `2`. -/
lemma toInteger_sub_one_not_dvd_two [IsCyclotomicExtension {p ^ (k + 1)} ‚Ñö K]
    (hŒ∂ : IsPrimitiveRoot Œ∂ (p ^ (k + 1))) (hodd : p ‚â† 2) : ¬¨ hŒ∂.toInteger - 1 ‚à£ 2 := fun h ‚Ü¶ by
  have : NumberField K := IsCyclotomicExtension.numberField {p ^ (k + 1)} ‚Ñö K
  replace h : hŒ∂.toInteger - 1 ‚à£ (2 : ‚Ñ§) := by simp [h]
  rw [‚Üê Ideal.norm_dvd_iff, hŒ∂.norm_toInteger_sub_one_of_prime_ne_two hodd] at h
  ¬∑ refine hodd <| (prime_dvd_prime_iff_eq ?_ ?_).1 ?_
    ¬∑ exact Nat.prime_iff.1 hp.1
    ¬∑ exact Nat.prime_iff.1 Nat.prime_two
    ¬∑ exact Int.ofNat_dvd.mp h
  ¬∑ rw [hŒ∂.norm_toInteger_sub_one_of_prime_ne_two hodd]
    exact Nat.prime_iff_prime_int.1 hp.1

open IntermediateField in
/--
Let `Œ∂` be a primitive root of unity of order `n` with `2 ‚â§ n`. Any prime number that divides the
norm, relative to `‚Ñ§`, of `Œ∂ - 1` divides also `n`.
-/
theorem prime_dvd_of_dvd_norm_sub_one {n : ‚Ñï} (hn : 2 ‚â§ n) {K : Type*}
    [Field K] [NumberField K] {Œ∂ : K} {p : ‚Ñï} [hF : Fact (Nat.Prime p)] (hŒ∂ : IsPrimitiveRoot Œ∂ n)
    (hp : let _ : NeZero n := NeZero.of_gt hn; (p : ‚Ñ§) ‚à£ norm ‚Ñ§ (hŒ∂.toInteger - 1)) :
    p ‚à£ n := by
  have : NeZero n := NeZero.of_gt hn
  obtain ‚ü®Œº, hC, hŒº, h‚ü© :
      ‚àÉ Œº : ‚Ñö‚üÆŒ∂‚üØ, ‚àÉ (_ : IsCyclotomicExtension {n} ‚Ñö ‚Ñö‚üÆŒ∂‚üØ), ‚àÉ (hŒº : IsPrimitiveRoot Œº n),
      norm ‚Ñ§ (hŒ∂.toInteger - 1) = norm ‚Ñ§ (hŒº.toInteger - 1) ^ Module.finrank ‚Ñö‚üÆŒ∂‚üØ K := by
    refine ‚ü®IntermediateField.AdjoinSimple.gen ‚Ñö Œ∂,
      intermediateField_adjoin_isCyclotomicExtension ‚Ñö hŒ∂, coe_submonoidClass_iff.mp hŒ∂, ?_‚ü©
    have : NumberField ‚Ñö‚üÆŒ∂‚üØ := of_intermediateField _
    rw [norm_eq_iff ‚Ñ§ (S‚Çò := K) (R‚Çò := ‚Ñö) le_rfl, map_sub, map_one, RingOfIntegers.map_mk,
      show Œ∂ - 1 = algebraMap ‚Ñö‚üÆŒ∂‚üØ K (IntermediateField.AdjoinSimple.gen ‚Ñö Œ∂ - 1) by rfl,
      ‚Üê norm_norm (S := ‚Ñö‚üÆŒ∂‚üØ), Algebra.norm_algebraMap, map_pow, map_pow, ‚Üê norm_localization ‚Ñ§
      (nonZeroDivisors ‚Ñ§) (S‚Çò := ‚Ñö‚üÆŒ∂‚üØ), map_sub (algebraMap _ _), RingOfIntegers.map_mk, map_one]
  dsimp only at hp
  rw [h] at hp
  rsuffices ‚ü®q, hq, t, s, ht‚ÇÅ, ht‚ÇÇ, hs‚ü© :
      ‚àÉ q, q.Prime ‚àß ‚àÉ t s, t ‚â† 0 ‚àß n = q ^ t ‚àß (p : ‚Ñ§) ‚à£ (q : ‚Ñ§) ^ s := by
    obtain hn | hn := lt_or_eq_of_le hn
    ¬∑ by_cases h : ‚àÉ q, q.Prime ‚àß ‚àÉ t, q ^ t = n
      ¬∑ obtain ‚ü®q, hq, t, hn'‚ü© := h
        have : Fact (Nat.Prime q) := ‚ü®hq‚ü©
        cases t with
        | zero => simp [‚Üê hn'] at hn
        | succ r =>
          rw [‚Üê hn'] at hC hŒº
          refine ‚ü®q, hq, r + 1, Module.finrank (‚Ñö‚üÆŒ∂‚üØ) K, r.add_one_ne_zero, hn'.symm, ?_‚ü©
          by_cases hq' : q = 2
          ¬∑ cases r with
            | zero =>
                rw [‚Üê hn', hq', zero_add, pow_one] at hn
                exact hn.false.elim
            | succ k =>
                rw [hq'] at hC hŒº ‚ä¢
                rwa [hŒº.norm_toInteger_sub_one_of_eq_two_pow] at hp
          ¬∑ rwa [hŒº.norm_toInteger_sub_one_of_prime_ne_two hq'] at hp
      ¬∑ rw [IsPrimitiveRoot.norm_toInteger_sub_one_eq_one hŒº hn, one_pow,
          Int.natCast_dvd_ofNat, Nat.dvd_one] at hp
        ¬∑ exact (Nat.Prime.ne_one hF.out hp).elim
        ¬∑ simp only [not_exists, not_and] at h
          exact fun {p} a k ‚Ü¶ h p a k
    ¬∑ rw [‚Üê hn] at hŒº hC ‚ä¢
      refine ‚ü®2, Nat.prime_two, 1, Module.finrank ‚Ñö‚üÆŒ∂‚üØ K, one_ne_zero, by rw [pow_one], ?_‚ü©
      rwa [hŒº.norm_toInteger_sub_one_of_eq_two, neg_eq_neg_one_mul, mul_pow, IsUnit.dvd_mul_left
        ((isUnit_pow_iff Module.finrank_pos.ne').mpr isUnit_neg_one)] at hp
  have : p = q := by
    rw [‚Üê Int.natCast_pow, Int.natCast_dvd_natCast] at hs
    exact (Nat.prime_dvd_prime_iff_eq hF.out hq).mp (hF.out.dvd_of_dvd_pow hs)
  rw [ht‚ÇÇ, this]
  exact dvd_pow_self _ ht‚ÇÅ

end IsPrimitiveRoot

section absdiscr

namespace IsCyclotomicExtension.Rat

open nonZeroDivisors IsPrimitiveRoot

variable (K p k)
variable [CharZero K]

/-- We compute the absolute discriminant of a `p ^ k`-th cyclotomic field.
  Beware that in the cases `p ^ k = 1` and `p ^ k = 2` the formula uses `1 / 2 = 0` and `0 - 1 = 0`.
  See also the results below. -/
theorem absdiscr_prime_pow [IsCyclotomicExtension {p ^ k} ‚Ñö K] :
    haveI : NumberField K := IsCyclotomicExtension.numberField {p ^ k} ‚Ñö K
    NumberField.discr K =
    (-1) ^ ((p ^ k).totient / 2) * p ^ (p ^ (k - 1) * ((p - 1) * k - 1)) := by
  have hŒ∂ := IsCyclotomicExtension.zeta_spec (p ^ k) ‚Ñö K
  have : NumberField K := IsCyclotomicExtension.numberField {p ^ k} ‚Ñö K
  let pB‚ÇÅ := integralPowerBasis hŒ∂
  apply (algebraMap ‚Ñ§ ‚Ñö).injective_int
  rw [‚Üê NumberField.discr_eq_discr _ pB‚ÇÅ.basis, ‚Üê Algebra.discr_localizationLocalization ‚Ñ§ ‚Ñ§‚Å∞ K]
  convert IsCyclotomicExtension.discr_prime_pow hŒ∂
    (cyclotomic.irreducible_rat (NeZero.pos _)) using 1
  ¬∑ have : pB‚ÇÅ.dim = (IsPrimitiveRoot.powerBasis ‚Ñö hŒ∂).dim := by
      rw [‚Üê PowerBasis.finrank, ‚Üê PowerBasis.finrank]
      exact RingOfIntegers.rank K
    rw [‚Üê Algebra.discr_reindex _ _ (finCongr this)]
    congr 1
    ext i
    simp_rw [Function.comp_apply, Module.Basis.localizationLocalization_apply, powerBasis_dim,
      PowerBasis.coe_basis, pB‚ÇÅ, integralPowerBasis_gen]
    convert ‚Üê ((IsPrimitiveRoot.powerBasis ‚Ñö hŒ∂).basis_eq_pow i).symm using 1
  ¬∑ simp_rw [algebraMap_int_eq, map_mul, map_pow, map_neg, map_one, map_natCast]

open Nat in
/-- We compute the absolute discriminant of a `p ^ (k + 1)`-th cyclotomic field.
  Beware that in the case `p ^ k = 2` the formula uses `1 / 2 = 0`. See also the results below. -/
theorem absdiscr_prime_pow_succ [IsCyclotomicExtension {p ^ (k + 1)} ‚Ñö K] :
    haveI : NumberField K := IsCyclotomicExtension.numberField {p ^ (k + 1)} ‚Ñö K
    NumberField.discr K =
    (-1) ^ (p ^ k * (p - 1) / 2) * p ^ (p ^ k * ((p - 1) * (k + 1) - 1)) := by
  simpa [totient_prime_pow hp.out (succ_pos k)] using absdiscr_prime_pow p (k + 1) K

/-- We compute the absolute discriminant of a `p`-th cyclotomic field where `p` is prime. -/
theorem absdiscr_prime [IsCyclotomicExtension {p} ‚Ñö K] :
    haveI : NumberField K := IsCyclotomicExtension.numberField {p} ‚Ñö K
    NumberField.discr K = (-1) ^ ((p - 1) / 2) * p ^ (p - 2) := by
  have : IsCyclotomicExtension {p ^ (0 + 1)} ‚Ñö K := by
    rw [zero_add, pow_one]
    infer_instance
  rw [absdiscr_prime_pow_succ p 0 K]
  simp [Nat.sub_sub]

end IsCyclotomicExtension.Rat

end absdiscr

end PowerBasis
