/-
Copyright (c) 2022 Riccardo Brasca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Riccardo Brasca
-/
import Mathlib.NumberTheory.Cyclotomic.Discriminant
import Mathlib.RingTheory.Polynomial.Eisenstein.IsIntegral
import Mathlib.RingTheory.Ideal.Norm.AbsNorm
import Mathlib.RingTheory.Prime

/-!
# Ring of integers of `p ^ n`-th cyclotomic fields
We gather results about cyclotomic extensions of `â„š`. In particular, we compute the ring of
integers of a `p ^ n`-th cyclotomic extension of `â„š`.

## Main results
* `IsCyclotomicExtension.Rat.isIntegralClosure_adjoin_singleton_of_prime_pow`: if `K` is a
  `p ^ k`-th cyclotomic extension of `â„š`, then `(adjoin â„¤ {Î¶})` is the integral closure of
  `â„¤` in `K`.
* `IsCyclotomicExtension.Rat.cyclotomicRing_isIntegralClosure_of_prime_pow`: the integral
  closure of `â„¤` inside `CyclotomicField (p ^ k) â„š` is `CyclotomicRing (p ^ k) â„¤ â„š`.
* `IsCyclotomicExtension.Rat.absdiscr_prime_pow` and related results: the absolute discriminant
  of cyclotomic fields.
-/

universe u

open Algebra IsCyclotomicExtension Polynomial NumberField

open scoped Cyclotomic Nat

variable {p : â„•} {k : â„•} {K : Type u} [Field K] {Î¶ : K} [hp : Fact p.Prime]

namespace IsCyclotomicExtension.Rat

variable [CharZero K]

/-- The discriminant of the power basis given by `Î¶ - 1`. -/
theorem discr_prime_pow_ne_two' [IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (k + 1))) (hk : p ^ (k + 1) â‰  2) :
    discr â„š (hÎ¶.subOnePowerBasis â„š).basis =
      (-1) ^ ((p ^ (k + 1)).totient / 2) * p ^ (p ^ k * ((p - 1) * (k + 1) - 1)) := by
  rw [â† discr_prime_pow_ne_two hÎ¶ (cyclotomic.irreducible_rat (NeZero.pos _)) hk]
  exact hÎ¶.discr_zeta_eq_discr_zeta_sub_one.symm

theorem discr_odd_prime' [IsCyclotomicExtension {p} â„š K] (hÎ¶ : IsPrimitiveRoot Î¶ p) (hodd : p â‰  2) :
    discr â„š (hÎ¶.subOnePowerBasis â„š).basis = (-1) ^ ((p - 1) / 2) * p ^ (p - 2) := by
  rw [â† discr_odd_prime hÎ¶ (cyclotomic.irreducible_rat hp.out.pos) hodd]
  exact hÎ¶.discr_zeta_eq_discr_zeta_sub_one.symm

/-- The discriminant of the power basis given by `Î¶ - 1`. Beware that in the cases `p ^ k = 1` and
`p ^ k = 2` the formula uses `1 / 2 = 0` and `0 - 1 = 0`. It is useful only to have a uniform
result. See also `IsCyclotomicExtension.Rat.discr_prime_pow_eq_unit_mul_pow'`. -/
theorem discr_prime_pow' [IsCyclotomicExtension {p ^ k} â„š K] (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ k)) :
    discr â„š (hÎ¶.subOnePowerBasis â„š).basis =
      (-1) ^ ((p ^ k).totient / 2) * p ^ (p^ (k - 1) * ((p - 1) * k - 1)) := by
  rw [â† discr_prime_pow hÎ¶ (cyclotomic.irreducible_rat (NeZero.pos _))]
  exact hÎ¶.discr_zeta_eq_discr_zeta_sub_one.symm

/-- If `p` is a prime and `IsCyclotomicExtension {p ^ k} K L`, then there are `u : â„¤Ë£` and
`n : â„•` such that the discriminant of the power basis given by `Î¶ - 1` is `u * p ^ n`. Often this is
enough and less cumbersome to use than `IsCyclotomicExtension.Rat.discr_prime_pow'`. -/
theorem discr_prime_pow_eq_unit_mul_pow' [IsCyclotomicExtension {p ^ k} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ k)) :
    âˆƒ (u : â„¤Ë£) (n : â„•), discr â„š (hÎ¶.subOnePowerBasis â„š).basis = u * p ^ n := by
  rw [hÎ¶.discr_zeta_eq_discr_zeta_sub_one.symm]
  exact discr_prime_pow_eq_unit_mul_pow hÎ¶ (cyclotomic.irreducible_rat (NeZero.pos _))

/-- If `K` is a `p ^ k`-th cyclotomic extension of `â„š`, then `(adjoin â„¤ {Î¶})` is the
integral closure of `â„¤` in `K`. -/
theorem isIntegralClosure_adjoin_singleton_of_prime_pow [hcycl : IsCyclotomicExtension {p ^ k} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ k)) : IsIntegralClosure (adjoin â„¤ ({Î¶} : Set K)) â„¤ K := by
  refine âŸ¨Subtype.val_injective, @fun x => âŸ¨fun h => âŸ¨âŸ¨x, ?_âŸ©, rflâŸ©, ?_âŸ©âŸ©
  swap
  Â· rintro âŸ¨y, rflâŸ©
    exact
      IsIntegral.algebraMap
        ((le_integralClosure_iff_isIntegral.1
          (adjoin_le_integralClosure (hÎ¶.isIntegral (NeZero.pos _)))).isIntegral _)
  let B := hÎ¶.subOnePowerBasis â„š
  have hint : IsIntegral â„¤ B.gen := (hÎ¶.isIntegral (NeZero.pos _)).sub isIntegral_one
  -- Porting note: the following `letI` was not needed because the locale `cyclotomic` set it
  -- as instances.
  letI := IsCyclotomicExtension.finiteDimensional {p ^ k} â„š K
  have H := discr_mul_isIntegral_mem_adjoin â„š hint h
  obtain âŸ¨u, n, hunâŸ© := discr_prime_pow_eq_unit_mul_pow' hÎ¶
  rw [hun] at H
  replace H := Subalgebra.smul_mem _ H u.inv
  rw [â† smul_assoc, â† smul_mul_assoc, Units.inv_eq_val_inv, zsmul_eq_mul, â† Int.cast_mul,
    Units.inv_mul, Int.cast_one, one_mul, smul_def, map_pow] at H
  cases k
  Â· haveI : IsCyclotomicExtension {1} â„š K := by simpa using hcycl
    have : x âˆˆ (âŠ¥ : Subalgebra â„š K) := by
      rw [singleton_one â„š K]
      exact mem_top
    obtain âŸ¨y, rflâŸ© := mem_bot.1 this
    replace h := (isIntegral_algebraMap_iff (algebraMap â„š K).injective).1 h
    obtain âŸ¨z, hzâŸ© := IsIntegrallyClosed.isIntegral_iff.1 h
    rw [â† hz, â† IsScalarTower.algebraMap_apply]
    exact Subalgebra.algebraMap_mem _ _
  Â· have hmin : (minpoly â„¤ B.gen).IsEisensteinAt (Submodule.span â„¤ {(p : â„¤)}) := by
      have hâ‚ := minpoly.isIntegrallyClosed_eq_field_fractions' â„š hint
      have hâ‚‚ := hÎ¶.minpoly_sub_one_eq_cyclotomic_comp (cyclotomic.irreducible_rat (NeZero.pos _))
      rw [IsPrimitiveRoot.subOnePowerBasis_gen] at hâ‚
      rw [hâ‚, â† map_cyclotomic_int, show Int.castRingHom â„š = algebraMap â„¤ â„š by rfl,
        show X + 1 = map (algebraMap â„¤ â„š) (X + 1) by simp, â† map_comp] at hâ‚‚
      rw [IsPrimitiveRoot.subOnePowerBasis_gen,
        map_injective (algebraMap â„¤ â„š) (algebraMap â„¤ â„š).injective_int hâ‚‚]
      exact cyclotomic_prime_pow_comp_X_add_one_isEisensteinAt p _
    refine
      adjoin_le ?_
        (mem_adjoin_of_smul_prime_pow_smul_of_minpoly_isEisensteinAt (n := n)
          (Nat.prime_iff_prime_int.1 hp.out) hint h (by simpa using H) hmin)
    simp only [Set.singleton_subset_iff, SetLike.mem_coe]
    exact Subalgebra.sub_mem _ (self_mem_adjoin_singleton â„¤ _) (Subalgebra.one_mem _)

theorem isIntegralClosure_adjoin_singleton_of_prime [hcycl : IsCyclotomicExtension {p} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ p) : IsIntegralClosure (adjoin â„¤ ({Î¶} : Set K)) â„¤ K := by
  rw [â† pow_one p] at hÎ¶ hcycl
  exact isIntegralClosure_adjoin_singleton_of_prime_pow hÎ¶

/-- The integral closure of `â„¤` inside `CyclotomicField (p ^ k) â„š` is
`CyclotomicRing (p ^ k) â„¤ â„š`. -/
theorem cyclotomicRing_isIntegralClosure_of_prime_pow :
    IsIntegralClosure (CyclotomicRing (p ^ k) â„¤ â„š) â„¤ (CyclotomicField (p ^ k) â„š) := by
  have hÎ¶ := zeta_spec (p ^ k) â„š (CyclotomicField (p ^ k) â„š)
  refine âŸ¨IsFractionRing.injective _ _, @fun x => âŸ¨fun h => âŸ¨âŸ¨x, ?_âŸ©, rflâŸ©, ?_âŸ©âŸ©
  Â· obtain âŸ¨y, rflâŸ© := (isIntegralClosure_adjoin_singleton_of_prime_pow hÎ¶).isIntegral_iff.1 h
    refine adjoin_mono ?_ y.2
    simp only [Set.singleton_subset_iff, Set.mem_setOf_eq]
    exact hÎ¶.pow_eq_one
  Â· rintro âŸ¨y, rflâŸ©
    exact IsIntegral.algebraMap ((IsCyclotomicExtension.integral {p ^ k} â„¤ _).isIntegral _)

theorem cyclotomicRing_isIntegralClosure_of_prime :
    IsIntegralClosure (CyclotomicRing p â„¤ â„š) â„¤ (CyclotomicField p â„š) := by
  rw [â† pow_one p]
  exact cyclotomicRing_isIntegralClosure_of_prime_pow

end IsCyclotomicExtension.Rat

section PowerBasis

open IsCyclotomicExtension.Rat

namespace IsPrimitiveRoot

section CharZero

variable [CharZero K]

/-- The algebra isomorphism `adjoin â„¤ {Î¶} â‰ƒâ‚[â„¤] (ğ“ K)`, where `Î¶` is a primitive `p ^ k`-th root of
unity and `K` is a `p ^ k`-th cyclotomic extension of `â„š`. -/
@[simps!]
noncomputable def _root_.IsPrimitiveRoot.adjoinEquivRingOfIntegers
    [IsCyclotomicExtension {p ^ k} â„š K] (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ k)) :
    adjoin â„¤ ({Î¶} : Set K) â‰ƒâ‚[â„¤] ğ“ K :=
  let _ := isIntegralClosure_adjoin_singleton_of_prime_pow hÎ¶
  IsIntegralClosure.equiv â„¤ (adjoin â„¤ ({Î¶} : Set K)) K (ğ“ K)

/-- The ring of integers of a `p ^ k`-th cyclotomic extension of `â„š` is a cyclotomic extension. -/
instance IsCyclotomicExtension.ringOfIntegers [IsCyclotomicExtension {p ^ k} â„š K] :
    IsCyclotomicExtension {p ^ k} â„¤ (ğ“ K) :=
  let _ := (zeta_spec (p ^ k) â„š K).adjoin_isCyclotomicExtension â„¤
  IsCyclotomicExtension.equiv _ â„¤ _ (zeta_spec (p ^ k) â„š K).adjoinEquivRingOfIntegers

/-- The integral `PowerBasis` of `ğ“ K` given by a primitive root of unity, where `K` is a `p ^ k`
cyclotomic extension of `â„š`. -/
noncomputable def integralPowerBasis [IsCyclotomicExtension {p ^ k} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ k)) : PowerBasis â„¤ (ğ“ K) :=
  (Algebra.adjoin.powerBasis' (hÎ¶.isIntegral (NeZero.pos _))).map hÎ¶.adjoinEquivRingOfIntegers

/-- Abbreviation to see a primitive root of unity as a member of the ring of integers. -/
abbrev toInteger {k : â„•} [NeZero k] (hÎ¶ : IsPrimitiveRoot Î¶ k) : ğ“ K :=
  âŸ¨Î¶, hÎ¶.isIntegral (NeZero.pos _)âŸ©

end CharZero

lemma coe_toInteger {k : â„•} [NeZero k] (hÎ¶ : IsPrimitiveRoot Î¶ k) : hÎ¶.toInteger.1 = Î¶ := rfl

/-- `ğ“ K â§¸ Ideal.span {Î¶ - 1}` is finite. -/
lemma finite_quotient_toInteger_sub_one [NumberField K] {k : â„•} (hk : 1 < k)
    (hÎ¶ : IsPrimitiveRoot Î¶ k) :
    have : NeZero k := NeZero.of_gt hk
    Finite (ğ“ K â§¸ Ideal.span {hÎ¶.toInteger - 1}) := by
  refine Ideal.finiteQuotientOfFreeOfNeBot _ (fun h â†¦ ?_)
  simp only [Ideal.span_singleton_eq_bot, sub_eq_zero] at h
  exact hÎ¶.ne_one hk (RingOfIntegers.ext_iff.1 h)

/-- We have that `ğ“ K â§¸ Ideal.span {Î¶ - 1}` has cardinality equal to the norm of `Î¶ - 1`.

See the results below to compute this norm in various cases. -/
lemma card_quotient_toInteger_sub_one [NumberField K] {k : â„•} [NeZero k]
    (hÎ¶ : IsPrimitiveRoot Î¶ k) :
    Nat.card (ğ“ K â§¸ Ideal.span {hÎ¶.toInteger - 1}) =
      (Algebra.norm â„¤ (hÎ¶.toInteger - 1)).natAbs := by
  rw [â† Submodule.cardQuot_apply, â† Ideal.absNorm_apply, Ideal.absNorm_span_singleton]

lemma toInteger_isPrimitiveRoot {k : â„•} [NeZero k] (hÎ¶ : IsPrimitiveRoot Î¶ k) :
    IsPrimitiveRoot hÎ¶.toInteger k :=
  IsPrimitiveRoot.of_map_of_injective (by exact hÎ¶) RingOfIntegers.coe_injective

variable [CharZero K]

@[simp]
theorem integralPowerBasis_gen [hcycl : IsCyclotomicExtension {p ^ k} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ k)) :
    hÎ¶.integralPowerBasis.gen = hÎ¶.toInteger :=
  Subtype.ext <| show algebraMap _ K hÎ¶.integralPowerBasis.gen = _ by
    rw [integralPowerBasis, PowerBasis.map_gen, adjoin.powerBasis'_gen]
    simp only [adjoinEquivRingOfIntegers_apply, IsIntegralClosure.algebraMap_lift]
    rfl

#adaptation_note /-- https://github.com/leanprover/lean4/pull/5338
We name `hcycl` so it can be used as a named argument,
but since https://github.com/leanprover/lean4/pull/5338, this is considered unused,
so we need to disable the linter. -/
set_option linter.unusedVariables false in
@[simp]
theorem integralPowerBasis_dim [hcycl : IsCyclotomicExtension {p ^ k} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ k)) : hÎ¶.integralPowerBasis.dim = Ï† (p ^ k) := by
  simp [integralPowerBasis, â† cyclotomic_eq_minpoly hÎ¶ (NeZero.pos _), natDegree_cyclotomic]

/-- The algebra isomorphism `adjoin â„¤ {Î¶} â‰ƒâ‚[â„¤] (ğ“ K)`, where `Î¶` is a primitive `p`-th root of
unity and `K` is a `p`-th cyclotomic extension of `â„š`. -/
@[simps!]
noncomputable def _root_.IsPrimitiveRoot.adjoinEquivRingOfIntegers'
    [hcycl : IsCyclotomicExtension {p} â„š K] (hÎ¶ : IsPrimitiveRoot Î¶ p) :
    adjoin â„¤ ({Î¶} : Set K) â‰ƒâ‚[â„¤] ğ“ K :=
  have : IsCyclotomicExtension {p ^ 1} â„š K := by convert hcycl; rw [pow_one]
  adjoinEquivRingOfIntegers (p := p) (k := 1) (Î¶ := Î¶) (by rwa [pow_one])

/-- The ring of integers of a `p`-th cyclotomic extension of `â„š` is a cyclotomic extension. -/
instance _root_.IsCyclotomicExtension.ring_of_integers' [IsCyclotomicExtension {p} â„š K] :
    IsCyclotomicExtension {p} â„¤ (ğ“ K) :=
  let _ := (zeta_spec p â„š K).adjoin_isCyclotomicExtension â„¤
  IsCyclotomicExtension.equiv _ â„¤ _ (zeta_spec p â„š K).adjoinEquivRingOfIntegers'

/-- The integral `PowerBasis` of `ğ“ K` given by a primitive root of unity, where `K` is a `p`-th
cyclotomic extension of `â„š`. -/
noncomputable def integralPowerBasis' [hcycl : IsCyclotomicExtension {p} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ p) : PowerBasis â„¤ (ğ“ K) :=
  have : IsCyclotomicExtension {p ^ 1} â„š K := by convert hcycl; rw [pow_one]
  integralPowerBasis (p := p) (k := 1) (Î¶ := Î¶) (by rwa [pow_one])

@[simp]
theorem integralPowerBasis'_gen [hcycl : IsCyclotomicExtension {p} â„š K] (hÎ¶ : IsPrimitiveRoot Î¶ p) :
    hÎ¶.integralPowerBasis'.gen = hÎ¶.toInteger :=
  integralPowerBasis_gen (hcycl := by rwa [pow_one]) (by rwa [pow_one])

@[simp]
theorem power_basis_int'_dim [hcycl : IsCyclotomicExtension {p} â„š K] (hÎ¶ : IsPrimitiveRoot Î¶ p) :
    hÎ¶.integralPowerBasis'.dim = Ï† p := by
  rw [integralPowerBasis', integralPowerBasis_dim (hcycl := by rwa [pow_one]) (by rwa [pow_one]),
    pow_one]


/-- The integral `PowerBasis` of `ğ“ K` given by `Î¶ - 1`, where `K` is a `p ^ k` cyclotomic
extension of `â„š`. -/
noncomputable def subOneIntegralPowerBasis [IsCyclotomicExtension {p ^ k} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ k)) : PowerBasis â„¤ (ğ“ K) :=
  PowerBasis.ofGenMemAdjoin' hÎ¶.integralPowerBasis (RingOfIntegers.isIntegral _)
    (by
      simp only [integralPowerBasis_gen, toInteger]
      convert Subalgebra.add_mem _ (self_mem_adjoin_singleton â„¤ (âŸ¨Î¶ - 1, _âŸ© : ğ“ K))
        (Subalgebra.one_mem _)
      Â· simp
      Â· exact Subalgebra.sub_mem _ (hÎ¶.isIntegral (NeZero.pos _)) (Subalgebra.one_mem _))

@[simp]
theorem subOneIntegralPowerBasis_gen [IsCyclotomicExtension {p ^ k} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ k)) :
    hÎ¶.subOneIntegralPowerBasis.gen =
      âŸ¨Î¶ - 1, Subalgebra.sub_mem _ (hÎ¶.isIntegral (NeZero.pos _)) (Subalgebra.one_mem _)âŸ© := by
  simp [subOneIntegralPowerBasis]

/-- The integral `PowerBasis` of `ğ“ K` given by `Î¶ - 1`, where `K` is a `p`-th cyclotomic
extension of `â„š`. -/
noncomputable def subOneIntegralPowerBasis' [IsCyclotomicExtension {p} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ p) : PowerBasis â„¤ (ğ“ K) :=
  have : IsCyclotomicExtension {p ^ 1} â„š K := by rwa [pow_one]
  subOneIntegralPowerBasis (p := p) (k := 1) (Î¶ := Î¶) (by rwa [pow_one])

@[simp, nolint unusedHavesSuffices]
theorem subOneIntegralPowerBasis'_gen [IsCyclotomicExtension {p} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ p) :
    hÎ¶.subOneIntegralPowerBasis'.gen = hÎ¶.toInteger - 1 :=
  -- The `unusedHavesSuffices` linter incorrectly thinks this `have` is unnecessary.
  have : IsCyclotomicExtension {p ^ 1} â„š K := by rwa [pow_one]
  subOneIntegralPowerBasis_gen (by rwa [pow_one])

/-- `Î¶ - 1` is prime if `p â‰  2` and `Î¶` is a primitive `p ^ (k + 1)`-th root of unity.
  See `zeta_sub_one_prime` for a general statement. -/
theorem zeta_sub_one_prime_of_ne_two [IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (k + 1))) (hodd : p â‰  2) :
    Prime (hÎ¶.toInteger - 1) := by
  letI := IsCyclotomicExtension.numberField {p ^ (k + 1)} â„š K
  refine Ideal.prime_of_irreducible_absNorm_span (fun h â†¦ ?_) ?_
  Â· apply hÎ¶.pow_ne_one_of_pos_of_lt zero_lt_one (one_lt_powâ‚€ hp.out.one_lt (by simp))
    rw [sub_eq_zero] at h
    simpa using congrArg (algebraMap _ K) h
  rw [Nat.irreducible_iff_prime, Ideal.absNorm_span_singleton, â† Nat.prime_iff,
    â† Int.prime_iff_natAbs_prime]
  convert Nat.prime_iff_prime_int.1 hp.out
  apply RingHom.injective_int (algebraMap â„¤ â„š)
  rw [â† Algebra.norm_localization (Sâ‚˜ := K) â„¤ (nonZeroDivisors â„¤)]
  simp only [algebraMap_int_eq, map_natCast]
  exact hÎ¶.norm_sub_one_of_prime_ne_two (Polynomial.cyclotomic.irreducible_rat (NeZero.pos _)) hodd

/-- `Î¶ - 1` is prime if `Î¶` is a primitive `2 ^ (k + 1)`-th root of unity.
  See `zeta_sub_one_prime` for a general statement. -/
theorem zeta_sub_one_prime_of_two_pow [IsCyclotomicExtension {2 ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (2 ^ (k + 1))) :
    Prime (hÎ¶.toInteger - 1) := by
  have := IsCyclotomicExtension.numberField {2 ^ (k + 1)} â„š K
  refine Ideal.prime_of_irreducible_absNorm_span (fun h â†¦ ?_) ?_
  Â· apply hÎ¶.pow_ne_one_of_pos_of_lt zero_lt_one (one_lt_powâ‚€ (by decide) (by simp))
    rw [sub_eq_zero] at h
    simpa using congrArg (algebraMap _ K) h
  rw [Nat.irreducible_iff_prime, Ideal.absNorm_span_singleton, â† Nat.prime_iff,
    â† Int.prime_iff_natAbs_prime]
  cases k
  Â· convert Prime.neg Int.prime_two
    apply RingHom.injective_int (algebraMap â„¤ â„š)
    rw [â† Algebra.norm_localization (Sâ‚˜ := K) â„¤ (nonZeroDivisors â„¤)]
    simp only [algebraMap_int_eq, map_neg, map_ofNat]
    simpa only [zero_add, pow_one, AddSubgroupClass.coe_sub, OneMemClass.coe_one,
        pow_zero]
      using hÎ¶.norm_pow_sub_one_two (cyclotomic.irreducible_rat
        (by simp only [zero_add, pow_one, Nat.ofNat_pos]))
  convert Int.prime_two
  apply RingHom.injective_int (algebraMap â„¤ â„š)
  rw [â† Algebra.norm_localization (Sâ‚˜ := K) â„¤ (nonZeroDivisors â„¤), algebraMap_int_eq]
  exact hÎ¶.norm_sub_one_two Nat.AtLeastTwo.prop (cyclotomic.irreducible_rat (by simp))

/-- `Î¶ - 1` is prime if `Î¶` is a primitive `p ^ (k + 1)`-th root of unity. -/
theorem zeta_sub_one_prime [IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (k + 1))) : Prime (hÎ¶.toInteger - 1) := by
  by_cases htwo : p = 2
  Â· subst htwo
    apply hÎ¶.zeta_sub_one_prime_of_two_pow
  Â· apply hÎ¶.zeta_sub_one_prime_of_ne_two htwo

/-- `Î¶ - 1` is prime if `Î¶` is a primitive `p`-th root of unity. -/
theorem zeta_sub_one_prime' [h : IsCyclotomicExtension {p} â„š K] (hÎ¶ : IsPrimitiveRoot Î¶ p) :
    Prime ((hÎ¶.toInteger - 1)) := by
  convert zeta_sub_one_prime (k := 0) (by simpa only [zero_add, pow_one])
  simpa only [zero_add, pow_one]

theorem subOneIntegralPowerBasis_gen_prime [IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (k + 1))) :
    Prime hÎ¶.subOneIntegralPowerBasis.gen := by
  simpa only [subOneIntegralPowerBasis_gen] using hÎ¶.zeta_sub_one_prime

theorem subOneIntegralPowerBasis'_gen_prime [IsCyclotomicExtension {p} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ p) :
    Prime hÎ¶.subOneIntegralPowerBasis'.gen := by
  simpa only [subOneIntegralPowerBasis'_gen] using hÎ¶.zeta_sub_one_prime'

/-- The norm, relative to `â„¤`, of `Î¶ ^ p ^ s - 1` in a `p ^ (k + 1)`-th cyclotomic extension of `â„š`
is p ^ p ^ s` if `s â‰¤ k` and `p ^ (k - s + 1) â‰  2`. -/
lemma norm_toInteger_pow_sub_one_of_prime_pow_ne_two [IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (k + 1))) {s : â„•} (hs : s â‰¤ k) (htwo : p ^ (k - s + 1) â‰  2) :
    Algebra.norm â„¤ (hÎ¶.toInteger ^ p ^ s - 1) = p ^ p ^ s := by
  have : NumberField K := IsCyclotomicExtension.numberField {p ^ (k + 1)} â„š K
  rw [Algebra.norm_eq_iff â„¤ (Sâ‚˜ := K) (Râ‚˜ := â„š) rfl.le]
  simp [hÎ¶.norm_pow_sub_one_of_prime_pow_ne_two (cyclotomic.irreducible_rat (NeZero.pos _)) hs htwo]

/-- The norm, relative to `â„¤`, of `Î¶ ^ 2 ^ k - 1` in a `2 ^ (k + 1)`-th cyclotomic extension of `â„š`
is `(-2) ^ 2 ^ k`. -/
lemma norm_toInteger_pow_sub_one_of_two [IsCyclotomicExtension {2 ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (2 ^ (k + 1))) :
    Algebra.norm â„¤ (hÎ¶.toInteger ^ 2 ^ k - 1) = (-2) ^ 2 ^ k := by
  have : NumberField K := IsCyclotomicExtension.numberField {2 ^ (k + 1)} â„š K
  rw [Algebra.norm_eq_iff â„¤ (Sâ‚˜ := K) (Râ‚˜ := â„š) rfl.le]
  simp [hÎ¶.norm_pow_sub_one_two (cyclotomic.irreducible_rat (pow_pos (by decide) _))]

/-- The norm, relative to `â„¤`, of `Î¶ ^ p ^ s - 1` in a `p ^ (k + 1)`-th cyclotomic extension of `â„š`
is `p ^ p ^ s` if `s â‰¤ k` and `p â‰  2`. -/
lemma norm_toInteger_pow_sub_one_of_prime_ne_two [IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (k + 1))) {s : â„•} (hs : s â‰¤ k) (hodd : p â‰  2) :
    Algebra.norm â„¤ (hÎ¶.toInteger ^ p ^ s - 1) = p ^ p ^ s := by
  refine hÎ¶.norm_toInteger_pow_sub_one_of_prime_pow_ne_two hs (fun h â†¦ hodd ?_)
  apply eq_of_prime_pow_eq hp.out.prime Nat.prime_two.prime (k - s).succ_pos
  rwa [pow_one]

/-- The norm, relative to `â„¤`, of `Î¶ - 1` in a `p ^ (k + 1)`-th cyclotomic extension of `â„š` is
`p` if `p â‰  2`. -/
lemma norm_toInteger_sub_one_of_prime_ne_two [IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (k + 1))) (hodd : p â‰  2) :
    Algebra.norm â„¤ (hÎ¶.toInteger - 1) = p := by
  simpa only [pow_zero, pow_one] using
    hÎ¶.norm_toInteger_pow_sub_one_of_prime_ne_two (Nat.zero_le _) hodd

/-- The norm, relative to `â„¤`, of `Î¶ - 1` in a `p`-th cyclotomic extension of `â„š` is `p` if
`p â‰  2`. -/
lemma norm_toInteger_sub_one_of_prime_ne_two' [hcycl : IsCyclotomicExtension {p} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ p) (h : p â‰  2) : Algebra.norm â„¤ (hÎ¶.toInteger - 1) = p := by
  have : IsCyclotomicExtension {p ^ (0 + 1)} â„š K := by simpa using hcycl
  replace hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (0 + 1)) := by simpa using hÎ¶
  exact hÎ¶.norm_toInteger_sub_one_of_prime_ne_two h

/-- The norm, relative to `â„¤`, of `Î¶ - 1` in a `p ^ (k + 1)`-th cyclotomic extension of `â„š` is
a prime if `p ^ (k  + 1) â‰  2`. -/
lemma prime_norm_toInteger_sub_one_of_prime_pow_ne_two [IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (k + 1))) (htwo : p ^ (k + 1) â‰  2) :
    Prime (Algebra.norm â„¤ (hÎ¶.toInteger - 1)) := by
  have := hÎ¶.norm_toInteger_pow_sub_one_of_prime_pow_ne_two (zero_le _) htwo
  simp only [pow_zero, pow_one] at this
  rw [this]
  exact Nat.prime_iff_prime_int.1 hp.out

/-- The norm, relative to `â„¤`, of `Î¶ - 1` in a `p ^ (k + 1)`-th cyclotomic extension of `â„š` is
a prime if `p â‰  2`. -/
lemma prime_norm_toInteger_sub_one_of_prime_ne_two [hcycl : IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (k + 1))) (hodd : p â‰  2) :
    Prime (Algebra.norm â„¤ (hÎ¶.toInteger - 1)) := by
  have := hÎ¶.norm_toInteger_sub_one_of_prime_ne_two hodd
  simp only [pow_zero, pow_one] at this
  rw [this]
  exact Nat.prime_iff_prime_int.1 hp.out

/-- The norm, relative to `â„¤`, of `Î¶ - 1` in a `p`-th cyclotomic extension of `â„š` is a prime if
`p â‰  2`. -/
lemma prime_norm_toInteger_sub_one_of_prime_ne_two' [hcycl : IsCyclotomicExtension {p} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ p) (hodd : p â‰  2) :
    Prime (Algebra.norm â„¤ (hÎ¶.toInteger - 1)) := by
  have : IsCyclotomicExtension {p ^ (0 + 1)} â„š K := by simpa using hcycl
  replace hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (0 + 1)) := by simpa using hÎ¶
  exact hÎ¶.prime_norm_toInteger_sub_one_of_prime_ne_two hodd

/-- In a `p ^ (k + 1)`-th cyclotomic extension of `â„š `, we have that `Î¶` is not congruent to an
  integer modulo `p` if `p ^ (k  + 1) â‰  2`. -/
theorem not_exists_int_prime_dvd_sub_of_prime_pow_ne_two
    [hcycl : IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (k + 1))) (htwo : p ^ (k + 1) â‰  2) :
    Â¬(âˆƒ n : â„¤, (p : ğ“ K) âˆ£ (hÎ¶.toInteger - n : ğ“ K)) := by
  intro âŸ¨n, x, hâŸ©
  -- Let `pB` be the power basis of `ğ“ K` given by powers of `Î¶`.
  let pB := hÎ¶.integralPowerBasis
  have hdim : pB.dim = p ^ k * (â†‘p - 1) := by
    simp [integralPowerBasis_dim, pB, Nat.totient_prime_pow hp.1 (Nat.zero_lt_succ k)]
  replace hdim : 1 < pB.dim := by
    rw [Nat.one_lt_iff_ne_zero_and_ne_one, hdim]
    refine âŸ¨by simp only [ne_eq, mul_eq_zero, NeZero.ne _, Nat.sub_eq_zero_iff_le, false_or,
      not_le, Nat.Prime.one_lt hp.out, pB], ne_of_gt ?_âŸ©
    by_cases hk : k = 0
    Â· simp only [hk, zero_add, pow_one, pow_zero, one_mul, Nat.lt_sub_iff_add_lt,
        Nat.reduceAdd] at htwo âŠ¢
      exact htwo.symm.lt_of_le hp.1.two_le
    Â· exact one_lt_mul_of_lt_of_le (one_lt_powâ‚€ hp.1.one_lt hk)
        (have := Nat.Prime.two_le hp.out; by omega)
  rw [sub_eq_iff_eq_add] at h
  -- We are assuming that `Î¶ = n + p * x` for some integer `n` and `x : ğ“ K`. Looking at the
  -- coordinates in the base `pB`, we obtain that `1` is a multiple of `p`, contradiction.
  replace h := pB.basis.ext_elem_iff.1 h âŸ¨1, hdimâŸ©
  have := pB.basis_eq_pow âŸ¨1, hdimâŸ©
  rw [hÎ¶.integralPowerBasis_gen] at this
  simp only [PowerBasis.coe_basis, pow_one] at this
  rw [â† this, show pB.gen = pB.gen ^ (âŸ¨1, hdimâŸ© : Fin pB.dim).1 by simp, â† pB.basis_eq_pow,
    pB.basis.repr_self_apply] at h
  simp only [â†“reduceIte, map_add, Finsupp.coe_add, Pi.add_apply] at h
  rw [show (p : ğ“ K) * x = (p : â„¤) â€¢ x by simp, â† pB.basis.coord_apply,
    LinearMap.map_smul, â† zsmul_one, â† pB.basis.coord_apply, LinearMap.map_smul,
    show 1 = pB.gen ^ (âŸ¨0, by omegaâŸ© : Fin pB.dim).1 by simp, â† pB.basis_eq_pow,
    pB.basis.coord_apply, pB.basis.coord_apply, pB.basis.repr_self_apply] at h
  simp only [smul_eq_mul, Fin.mk.injEq, zero_ne_one, â†“reduceIte, mul_zero, add_zero] at h
  exact (Int.prime_iff_natAbs_prime.2 (by simp [hp.1])).not_dvd_one âŸ¨_, hâŸ©

/-- In a `p ^ (k + 1)`-th cyclotomic extension of `â„š `, we have that `Î¶` is not congruent to an
  integer modulo `p` if `p â‰  2`. -/
theorem not_exists_int_prime_dvd_sub_of_prime_ne_two
    [hcycl : IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (k + 1))) (hodd : p â‰  2) :
    Â¬(âˆƒ n : â„¤, (p : ğ“ K) âˆ£ (hÎ¶.toInteger - n : ğ“ K)) := by
  refine not_exists_int_prime_dvd_sub_of_prime_pow_ne_two hÎ¶ (fun h â†¦ ?_)
  simp_all only [(@Nat.Prime.pow_eq_iff 2 p (k+1) Nat.prime_two).mp (by assumption_mod_cast),
    pow_one, ne_eq]

/-- In a `p`-th cyclotomic extension of `â„š `, we have that `Î¶` is not congruent to an
  integer modulo `p` if `p â‰  2`. -/
theorem not_exists_int_prime_dvd_sub_of_prime_ne_two'
    [hcycl : IsCyclotomicExtension {p} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ p) (hodd : p â‰  2) :
    Â¬(âˆƒ n : â„¤, (p : ğ“ K) âˆ£ (hÎ¶.toInteger - n : ğ“ K)) := by
  have : IsCyclotomicExtension {p ^ (0 + 1)} â„š K := by simpa using hcycl
  replace hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (0 + 1)) := by simpa using hÎ¶
  exact not_exists_int_prime_dvd_sub_of_prime_ne_two hÎ¶ hodd

theorem finite_quotient_span_sub_one [hcycl : IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (k + 1))) :
    Finite (ğ“ K â§¸ Ideal.span {hÎ¶.toInteger - 1}) := by
  have : NumberField K := IsCyclotomicExtension.numberField {p ^ (k + 1)} â„š K
  refine Ideal.finiteQuotientOfFreeOfNeBot _ (fun h â†¦ ?_)
  simp only [Ideal.span_singleton_eq_bot, sub_eq_zero, â† Subtype.coe_inj] at h
  exact hÎ¶.ne_one (one_lt_powâ‚€ hp.1.one_lt (Nat.zero_ne_add_one k).symm)
    (RingOfIntegers.ext_iff.1 h)

theorem finite_quotient_span_sub_one' [hcycl : IsCyclotomicExtension {p} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ p) :
    Finite (ğ“ K â§¸ Ideal.span {hÎ¶.toInteger - 1}) := by
  have : IsCyclotomicExtension {p ^ (0 + 1)} â„š K := by simpa using hcycl
  replace hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (0 + 1)) := by simpa using hÎ¶
  exact hÎ¶.finite_quotient_span_sub_one

/-- In a `p ^ (k + 1)`-th cyclotomic extension of `â„š`, we have that
  `Î¶ - 1` divides `p` in `ğ“ K`. -/
lemma toInteger_sub_one_dvd_prime [hcycl : IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (k + 1))) : ((hÎ¶.toInteger - 1)) âˆ£ p := by
  by_cases htwo : p ^ (k + 1) = 2
  Â· have âŸ¨hp2, hkâŸ© := (Nat.Prime.pow_eq_iff Nat.prime_two).1 htwo
    simp only [add_eq_right] at hk
    have hÎ¶' : Î¶ = -1 := by
      refine IsPrimitiveRoot.eq_neg_one_of_two_right ?_
      rwa [hk, zero_add, pow_one, hp2] at hÎ¶
    replace hÎ¶' : hÎ¶.toInteger = -1 := by
      ext
      exact hÎ¶'
    rw [hÎ¶', hp2]
    exact âŸ¨-1, by ringâŸ©
  suffices (hÎ¶.toInteger - 1) âˆ£ (p : â„¤) by simpa
  have := IsCyclotomicExtension.numberField {p ^ (k + 1)} â„š K
  have H := hÎ¶.norm_toInteger_pow_sub_one_of_prime_pow_ne_two (zero_le _) htwo
  rw [pow_zero, pow_one] at H
  rw [â† Ideal.norm_dvd_iff, H]
  Â· simp
  Â· exact prime_norm_toInteger_sub_one_of_prime_pow_ne_two hÎ¶ htwo

/-- In a `p`-th cyclotomic extension of `â„š`, we have that `Î¶ - 1` divides `p` in `ğ“ K`. -/
lemma toInteger_sub_one_dvd_prime' [hcycl : IsCyclotomicExtension {p} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ p) : hÎ¶.toInteger - 1 âˆ£ p := by
  have : IsCyclotomicExtension {p ^ (0 + 1)} â„š K := by simpa using hcycl
  replace hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (0 + 1)) := by simpa using hÎ¶
  exact toInteger_sub_one_dvd_prime hÎ¶

/-- We have that `hÎ¶.toInteger - 1` does not divide `2`. -/
lemma toInteger_sub_one_not_dvd_two [IsCyclotomicExtension {p ^ (k + 1)} â„š K]
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (k + 1))) (hodd : p â‰  2) : Â¬ hÎ¶.toInteger - 1 âˆ£ 2 := fun h â†¦ by
  have : NumberField K := IsCyclotomicExtension.numberField {p ^ (k + 1)} â„š K
  replace h : hÎ¶.toInteger - 1 âˆ£ (2 : â„¤) := by simp [h]
  rw [â† Ideal.norm_dvd_iff, hÎ¶.norm_toInteger_sub_one_of_prime_ne_two hodd] at h
  Â· refine hodd <| (prime_dvd_prime_iff_eq ?_ ?_).1 ?_
    Â· exact Nat.prime_iff.1 hp.1
    Â· exact Nat.prime_iff.1 Nat.prime_two
    Â· exact Int.ofNat_dvd.mp h
  Â· rw [hÎ¶.norm_toInteger_sub_one_of_prime_ne_two hodd]
    exact Nat.prime_iff_prime_int.1 hp.1

end IsPrimitiveRoot

section absdiscr

namespace IsCyclotomicExtension.Rat

open nonZeroDivisors IsPrimitiveRoot

variable (K p k)
variable [CharZero K]

/-- We compute the absolute discriminant of a `p ^ k`-th cyclotomic field.
  Beware that in the cases `p ^ k = 1` and `p ^ k = 2` the formula uses `1 / 2 = 0` and `0 - 1 = 0`.
  See also the results below. -/
theorem absdiscr_prime_pow [IsCyclotomicExtension {p ^ k} â„š K] :
    haveI : NumberField K := IsCyclotomicExtension.numberField {p ^ k} â„š K
    NumberField.discr K =
    (-1) ^ ((p ^ k).totient / 2) * p ^ (p ^ (k - 1) * ((p - 1) * k - 1)) := by
  have hÎ¶ := IsCyclotomicExtension.zeta_spec (p ^ k) â„š K
  have : NumberField K := IsCyclotomicExtension.numberField {p ^ k} â„š K
  let pBâ‚ := integralPowerBasis hÎ¶
  apply (algebraMap â„¤ â„š).injective_int
  rw [â† NumberField.discr_eq_discr _ pBâ‚.basis, â† Algebra.discr_localizationLocalization â„¤ â„¤â° K]
  convert IsCyclotomicExtension.discr_prime_pow hÎ¶
    (cyclotomic.irreducible_rat (NeZero.pos _)) using 1
  Â· have : pBâ‚.dim = (IsPrimitiveRoot.powerBasis â„š hÎ¶).dim := by
      rw [â† PowerBasis.finrank, â† PowerBasis.finrank]
      exact RingOfIntegers.rank K
    rw [â† Algebra.discr_reindex _ _ (finCongr this)]
    congr 1
    ext i
    simp_rw [Function.comp_apply, Basis.localizationLocalization_apply, powerBasis_dim,
      PowerBasis.coe_basis, pBâ‚, integralPowerBasis_gen]
    convert â† ((IsPrimitiveRoot.powerBasis â„š hÎ¶).basis_eq_pow i).symm using 1
  Â· simp_rw [algebraMap_int_eq, map_mul, map_pow, map_neg, map_one, map_natCast]

open Nat in
/-- We compute the absolute discriminant of a `p ^ (k + 1)`-th cyclotomic field.
  Beware that in the case `p ^ k = 2` the formula uses `1 / 2 = 0`. See also the results below. -/
theorem absdiscr_prime_pow_succ [IsCyclotomicExtension {p ^ (k + 1)} â„š K] :
    haveI : NumberField K := IsCyclotomicExtension.numberField {p ^ (k + 1)} â„š K
    NumberField.discr K =
    (-1) ^ (p ^ k * (p - 1) / 2) * p ^ (p ^ k * ((p - 1) * (k + 1) - 1)) := by
  simpa [totient_prime_pow hp.out (succ_pos k)] using absdiscr_prime_pow p (k + 1) K

/-- We compute the absolute discriminant of a `p`-th cyclotomic field where `p` is prime. -/
theorem absdiscr_prime [IsCyclotomicExtension {p} â„š K] :
    haveI : NumberField K := IsCyclotomicExtension.numberField {p} â„š K
    NumberField.discr K = (-1) ^ ((p - 1) / 2) * p ^ (p - 2) := by
  have : IsCyclotomicExtension {p ^ (0 + 1)} â„š K := by
    rw [zero_add, pow_one]
    infer_instance
  rw [absdiscr_prime_pow_succ p 0 K]
  simp only [Int.reduceNeg, pow_zero, one_mul, zero_add, mul_one, mul_eq_mul_left_iff,
    pow_eq_zero_iff', neg_eq_zero, one_ne_zero, ne_eq, false_and, or_false]
  rfl

end IsCyclotomicExtension.Rat

end absdiscr

end PowerBasis
