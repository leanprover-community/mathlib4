/-
Copyright (c) 2024 Riccardo Brasca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Riccardo Brasca
-/

import Mathlib.NumberTheory.Cyclotomic.Embeddings
import Mathlib.NumberTheory.Cyclotomic.Rat
import Mathlib.NumberTheory.NumberField.Units

/-!
# Third cyclotomic field.
We gather various results about the third cyclotomic field.

## Main results
* `IsCyclotomicExtension.Rat.Three.Units.mem`: Given a unit `u : (ğ“ K)Ë£`, where `K` is a number
field such that `IsCyclotomicExtension {3} â„š K`, then `u âˆˆ ({1, -1, Î¶, -Î¶, Î¶^2, -Î¶^2}`, where `Î¶`
is any primitive `3`-rd root of unity in `K`.

* `IsCyclotomicExtension.Rat.Three.Units.mem.eq_one_or_neg_one_of_unit_of_congruent`: Given a unit
`u : (ğ“ K)Ë£`, where `K` is a number field such that `IsCyclotomicExtension {3} â„š K`, if `u` is
congruent to an integer modulo `3`, then `u = 1` or `u = -1`. This is a special case of the
so-called *Kummer's lemma*.

-/

open NumberField Units InfinitePlace nonZeroDivisors Polynomial

namespace IsCyclotomicExtension.Rat.Three

variable {K : Type*} [Field K] [NumberField K] [IsCyclotomicExtension {3} â„š K]
variable {Î¶ : K} (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(3 : â„•+)) (u : (ğ“ K)Ë£)

local notation3 "Î·" => hÎ¶.toInteger

local notation3 "Î»" => hÎ¶.toInteger - 1

local notation3 "f" => algebraMap (ğ“ K) (ğ“ K â§¸ Ideal.span {Î»})

/-- Given a unit `u : (ğ“ K)Ë£`, where `K` is a number field such that
`IsCyclotomicExtension {3} â„š K`, then `u âˆˆ ({1, -1, Î¶, -Î¶, Î¶^2, -Î¶^2}`, where `Î¶` is any
primitive `3`-rd root of unity in `K`. -/
theorem Units.mem : â†‘u âˆˆ({1, -1, Î·, -Î·, Î· ^ 2, -Î· ^ 2} : Set (ğ“ K)) := by
  have hrank : rank K = 0 := by
    dsimp [rank]
    rw [card_eq_NrRealPlaces_add_NrComplexPlaces, nrRealPlaces_eq_zero (n := 3) K (by decide),
      zero_add, nrComplexPlaces_eq_totient_div_two (n := 3)]
    rfl
  obtain âŸ¨x, âŸ¨_, hxu, -âŸ©, -âŸ© := exist_unique_eq_mul_prod _ u
  replace hxu : u = x := by
    rw [â† mul_one x.1]
    convert hxu
    convert Finset.prod_empty.symm
    rw [Finset.univ_eq_empty_iff, hrank]
    infer_instance
  obtain âŸ¨n, hnpos, hnâŸ© := isOfFinOrder_iff_pow_eq_one.1 <| (CommGroup.mem_torsion _ _).1 x.2
  replace hn : (â†‘u : K) ^ ((âŸ¨n, hnposâŸ© : â„•+) : â„•) = 1 := by
    norm_cast
    simp [hxu, hn]
  have hodd : Odd ((3 : â„•+) : â„•) := by decide
  obtain âŸ¨r, hr3, hruâŸ© := hÎ¶.exists_pow_or_neg_mul_pow_of_isOfFinOrder hodd
    (isOfFinOrder_iff_pow_eq_one.2 âŸ¨n, hnpos, hnâŸ©)
  replace hr : r âˆˆ Finset.Ico 0 3 := Finset.mem_Ico.2 âŸ¨by simp, hr3âŸ©
  replace hru : â†‘u = Î· ^ r âˆ¨ â†‘u = -Î· ^ r := by
    rcases hru with (h | h)
    Â· left; ext; exact h
    Â· right; ext; exact h
  fin_cases hr
  Â· rcases hru with (h | h) <;> simp [h]
  Â· rcases hru with (h | h) <;> simp [h]
  Â· rcases hru with (h | h)
    Â· apply Set.mem_insert_of_mem; apply Set.mem_insert_of_mem; simp [h]
    Â· apply Set.mem_insert_of_mem; apply Set.mem_insert_of_mem; simp [h]

theorem Units.not_exists_int_three_dvd_sub : Â¬(âˆƒ n : â„¤, (3 : ğ“ K) âˆ£ (Î· - n : ğ“ K)) := by
  intro âŸ¨n, x, hâŸ©
  let pB := hÎ¶.integralPowerBasis'
  have hdim : pB.dim = 2 := by
    simp only [IsPrimitiveRoot.power_basis_int'_dim, PNat.val_ofNat, Nat.reduceSucc, pB]
    rfl
  replace hdim : 1 < pB.dim := by simp [hdim]
  rw [sub_eq_iff_eq_add] at h
  replace h := pB.basis.ext_elem_iff.1 h âŸ¨1, hdimâŸ©
  have := pB.basis_eq_pow âŸ¨1, hdimâŸ©
  rw [hÎ¶.integralPowerBasis'_gen] at this
  simp only [PowerBasis.coe_basis, pow_one] at this
  rw [â† this, show pB.gen = pB.gen ^ (âŸ¨1, hdimâŸ©: Fin pB.dim).1 by simp, â† pB.basis_eq_pow,
    pB.basis.repr_self_apply] at h
  simp only [â†“reduceIte, map_add, Finsupp.coe_add, Pi.add_apply] at h
  rw [show (3 : ğ“ K) * x = (3 : â„¤) â€¢ x by simp, â† pB.basis.coord_apply,
    LinearMap.map_smul, â† zsmul_one, â† pB.basis.coord_apply, LinearMap.map_smul,
    show 1 = pB.gen ^ (âŸ¨0, by linarithâŸ©: Fin pB.dim).1 by simp, â† pB.basis_eq_pow,
    pB.basis.coord_apply, pB.basis.coord_apply, pB.basis.repr_self_apply] at h
  simp only [smul_eq_mul, Fin.mk.injEq, zero_ne_one, â†“reduceIte, mul_zero, add_zero] at h
  have hdvd : Â¬ ((3 : â„¤) âˆ£ 1) := by norm_num
  apply hdvd
  exact âŸ¨_, hâŸ©

lemma lambda_sq : Î» ^ 2 = -3 * Î· :=
  calc Î» ^ 2 = Î· ^ 2 + Î· + 1 - 3 * Î· := by ring
  _ = 0 - 3 * Î· := by ext; simpa using hÎ¶.isRoot_cyclotomic (by decide)
  _ = -3 * Î· := by ring

/-- Given a unit `u : (ğ“ K)Ë£`, where `K` is a number field such that
`IsCyclotomicExtension {3} â„š K`, if `u` is congruent to an integer modulo `Î» ^ 2`, then `u = 1` or
`u = -1`.

This is a special case of the so-called *Kummer's lemma*. -/
theorem eq_one_or_neg_one_of_unit_of_congruent (hcong : âˆƒ n : â„¤, Î» ^ 2 âˆ£ (â†‘u - n : ğ“ K)) :
    u = 1 âˆ¨ u = -1 := by
  replace hcong : âˆƒ n : â„¤, (3 : ğ“ K) âˆ£ (â†‘u - n : ğ“ K) := by
    obtain âŸ¨n, x, hxâŸ© := hcong
    exact âŸ¨n, -Î· * x, by rw [â† mul_assoc, mul_neg, â† neg_mul, â† lambda_sq, hx]âŸ©
  have hÎ¶ := IsCyclotomicExtension.zeta_spec 3 â„š K
  have := Units.mem hÎ¶ u
  have h2 : (hÎ¶.pow_of_coprime 2 (by decide)).toInteger = hÎ¶.toInteger ^ 2 := by ext; simp
  simp only [Set.mem_insert_iff, val_eq_one, Set.mem_singleton_iff] at this
  rcases this with (rfl | h | h | h | h | h)
  Â· left; rfl
  Â· right; ext; simp [h]
  Â· exfalso
    apply Units.not_exists_int_three_dvd_sub hÎ¶
    rw [â† h]
    exact hcong
  Â· exfalso
    apply Units.not_exists_int_three_dvd_sub hÎ¶
    obtain âŸ¨n, x, hxâŸ© := hcong
    rw [sub_eq_iff_eq_add] at hx
    refine âŸ¨-n, -x, ?_âŸ©
    rw [â† neg_eq_iff_eq_neg.2 h, hx]
    simp
  Â· exfalso
    apply Units.not_exists_int_three_dvd_sub <| hÎ¶.pow_of_coprime 2 (by decide)
    rw [h2, â† h]
    exact hcong
  Â· exfalso
    apply Units.not_exists_int_three_dvd_sub <| hÎ¶.pow_of_coprime 2 (by decide)
    obtain âŸ¨n, x, hxâŸ© := hcong
    refine âŸ¨-n, -x, ?_âŸ©
    rw [h2, mul_neg, â† hx, â† neg_eq_iff_eq_neg.2 h]
    simp only [Int.cast_neg, sub_neg_eq_add, neg_sub]
    ring

noncomputable
instance : Fintype (ğ“ K â§¸ Ideal.span {Î»}) := by
  refine Ideal.fintypeQuotientOfFreeOfNeBot _ (fun h â†¦ ?_)
  simp only [Ideal.span_singleton_eq_bot, sub_eq_zero, â† Subtype.coe_inj] at h
  exact hÎ¶.ne_one (by decide) h

lemma norm_lambda : Algebra.norm â„¤ Î» = 3 := by
  apply (algebraMap â„¤ â„š).injective_int
  have : algebraMap (ğ“ K) K Î» = Î¶ - 1 := by
    simp only [map_sub, map_one, sub_left_inj]
    exact rfl
  rw [â† Algebra.norm_localization (Sâ‚˜ := K) â„¤ â„¤â°, this, hÎ¶.sub_one_norm_prime
    (cyclotomic.irreducible_rat (n := 3) (by decide)) (by decide)]
  simp

lemma norm_lambda_prime : Prime (Algebra.norm â„¤ Î») := by
  rw [norm_lambda]
  exact Int.prime_three

lemma lambda_dvd_three : Î» âˆ£ 3 := by
  suffices Î» âˆ£ (3 : â„¤) by simpa
  rw [â† Ideal.norm_dvd_iff, norm_lambda hÎ¶]
  rw [norm_lambda hÎ¶]
  exact Int.prime_three

lemma lambda_not_unit : Â¬ IsUnit Î» := by
  intro h
  rw [isUnit_iff_dvd_one, show (1 : ğ“ K) = ((1 : â„¤) : ğ“ K) by simp, show Î· - ((1 : â„¤) : ğ“ K) = Î»
    by simp, â† Ideal.norm_dvd_iff (norm_lambda_prime hÎ¶), norm_lambda hÎ¶] at h
  have h3 : Â¬((3 : â„¤) âˆ£ 1) := by decide
  exact h3 h

lemma card_quot : Fintype.card (ğ“ K â§¸ Ideal.span {Î»}) = 3 := by
  rw [â† Submodule.cardQuot_apply, â† Ideal.absNorm_apply, Ideal.absNorm_span_singleton]
  simp [norm_lambda hÎ¶]

lemma two_ne_zero : (2 : ğ“ K â§¸ Ideal.span {Î»}) â‰  0 := by
  suffices 2 âˆ‰ Ideal.span {Î»} by
    intro h
    refine this (Ideal.Quotient.eq_zero_iff_mem.1 <| by simp [h])
  intro h
  rw [Ideal.mem_span_singleton] at h
  replace h : Î» âˆ£ â†‘(2 : â„¤) := by simp [h]
  rw [â† Ideal.norm_dvd_iff, norm_lambda hÎ¶] at h
  Â· norm_num at h
  Â· rw [norm_lambda hÎ¶]
    exact Int.prime_three

lemma lambda_not_dvd_two : Â¬ Î» âˆ£ 2 := by
  intro h
  exact two_ne_zero hÎ¶ (Ideal.Quotient.eq_zero_iff_mem.2 <| Ideal.mem_span_singleton.2 h)

instance : Nontrivial (ğ“ K â§¸ Ideal.span {Î»}) := nontrivial_of_ne 2 0 <| two_ne_zero hÎ¶

open Classical Finset in
lemma univ_quot : (univ : Finset ((ğ“ K â§¸ Ideal.span {Î»}))) = {0, 1, -1} := by
  refine (eq_of_subset_of_card_le (fun _ _ â†¦ mem_univ _) ?_).symm
  rw [card_univ, card_quot hÎ¶, card_insert_of_not_mem, card_insert_of_not_mem, card_singleton]
  Â· rw [mem_singleton]
    intro h
    rw [â† add_eq_zero_iff_eq_neg, one_add_one_eq_two] at h
    exact two_ne_zero hÎ¶ h
  Â· intro h
    simp only [mem_insert, mem_singleton, zero_eq_neg] at h
    rcases h with (h | h)
    Â· exact zero_ne_one h
    Â· exact zero_ne_one h.symm

lemma dvd_or_dvd_sub_one_or_dvd_add_one (x : ğ“ K) : Î» âˆ£ x âˆ¨ Î» âˆ£ x - 1 âˆ¨ Î» âˆ£ x + 1 := by
  have := Finset.mem_univ (Ideal.Quotient.mk (Ideal.span {Î»}) x)
  rw [univ_quot hÎ¶] at this
  simp only [Finset.mem_insert, Finset.mem_singleton] at this
  rcases this with (h | h | h)
  Â· left
    exact Ideal.mem_span_singleton.1 <| Ideal.Quotient.eq_zero_iff_mem.1 h
  Â· right; left
    refine Ideal.mem_span_singleton.1 <| Ideal.Quotient.eq_zero_iff_mem.1 ?_
    rw [RingHom.map_sub, h, RingHom.map_one, sub_self]
  Â· right; right
    refine Ideal.mem_span_singleton.1 <| Ideal.Quotient.eq_zero_iff_mem.1 ?_
    rw [RingHom.map_add, h, RingHom.map_one, add_left_neg]

lemma _root_.IsPrimitiveRoot.toInteger_coe : hÎ¶.toInteger.1 = Î¶ := rfl

lemma _root_.IsPrimitiveRoot.toInteger_cube_eq_one : Î· ^ 3 = 1 := by
  ext
  simp only [SubmonoidClass.coe_pow, OneMemClass.coe_one]
  exact hÎ¶.pow_eq_one

lemma _root_.IsPrimitiveRoot.eta_isUnit : IsUnit Î· :=
  isUnit_of_mul_eq_one _ (Î· ^ 2) (by simp [â† pow_succ, hÎ¶.toInteger_cube_eq_one])

lemma _root_.IsPrimitiveRoot.toInteger_eval_cyclo : Î· ^ 2 + Î· + 1 = 0 := by
  ext; simpa using hÎ¶.isRoot_cyclotomic (by decide)

lemma cube_sub_one (x : ğ“ K) : x ^ 3 - 1 = (x - 1) * (x - Î·) * (x - Î· ^ 2) := by
  symm
  calc _ = x ^ 3 - x ^ 2 * (Î· ^ 2 + Î· + 1) + x * (Î· ^ 2 + Î· + Î· ^ 3) - Î· ^ 3 := by ring
  _ = x ^ 3 - x ^ 2 * (Î· ^ 2 + Î· + 1) + x * (Î· ^ 2 + Î· + 1) - 1 := by rw [hÎ¶.toInteger_cube_eq_one]
  _ = x ^ 3 - 1 := by rw [hÎ¶.toInteger_eval_cyclo]; ring

lemma lambda_dvd_mul_sub_one_mul_sub_eta_add_one (x : ğ“ K) :
    Î» âˆ£ x * (x - 1) * (x - (Î· + 1)) := by
  rcases dvd_or_dvd_sub_one_or_dvd_add_one hÎ¶ x with (h | h | h)
  Â· exact dvd_mul_of_dvd_left (dvd_mul_of_dvd_left h _) _
  Â· exact dvd_mul_of_dvd_left (dvd_mul_of_dvd_right h _) _
  Â· refine dvd_mul_of_dvd_right ?_ _
    rw [show x - (Î· + 1) = x + 1 - (Î· - 1 + 3) by ring]
    exact dvd_sub h (dvd_add dvd_rfl <| lambda_dvd_three hÎ¶)

lemma lambda_pow_four_dvd_cube_sub_one_of_dvd_sub_one {x : ğ“ K} (h : Î» âˆ£ x - 1) :
    Î» ^ 4 âˆ£ x ^ 3 - 1 := by
  obtain âŸ¨y, hyâŸ© := h
  have : x ^ 3 - 1 = Î» ^ 3 * (y * (y - 1) * (y - (Î· + 1))) := by
    calc _ =  (x - 1) * (x - 1 - Î») * (x - 1 - Î» * (Î· + 1)) := by rw [cube_sub_one hÎ¶ x]; ring
    _ = _ := by rw [hy]; ring
  rw [this, show Î» ^ 4 = Î» ^ 3 * Î» by ring]
  exact mul_dvd_mul dvd_rfl (lambda_dvd_mul_sub_one_mul_sub_eta_add_one hÎ¶ y)

lemma lambda_pow_four_dvd_cube_add_one_of_dvd_add_one {x : ğ“ K} (h : Î» âˆ£ x + 1) :
    Î» ^ 4 âˆ£ x ^ 3 + 1 := by
  replace h : Î» âˆ£ -x - 1 := by
    obtain âŸ¨y, hyâŸ© := h
    refine âŸ¨-y, ?_âŸ©
    rw [mul_neg, â† hy]
    ring
  obtain âŸ¨y, hyâŸ© := lambda_pow_four_dvd_cube_sub_one_of_dvd_sub_one hÎ¶ h
  refine âŸ¨-y, ?_âŸ©
  rw [mul_neg, â† hy]
  ring

lemma lambda_pow_four_dvd_cube_sub_one_or_add_one_of_lambda_not_dvd {x : ğ“ K} (h : Â¬ Î» âˆ£ x) :
    Î» ^ 4 âˆ£ x ^ 3 - 1 âˆ¨ Î» ^ 4 âˆ£ x ^ 3 + 1 := by
  rcases dvd_or_dvd_sub_one_or_dvd_add_one hÎ¶ x with (H | H | H)
  Â· contradiction
  Â· left
    exact lambda_pow_four_dvd_cube_sub_one_of_dvd_sub_one hÎ¶ H
  Â· right
    exact lambda_pow_four_dvd_cube_add_one_of_dvd_add_one hÎ¶ H

end IsCyclotomicExtension.Rat.Three
