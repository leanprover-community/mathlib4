/-
Copyright (c) 2024 Riccardo Brasca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Riccardo Brasca, Pietro Monticone
-/

import Mathlib.NumberTheory.Cyclotomic.Embeddings
import Mathlib.NumberTheory.Cyclotomic.Rat
import Mathlib.NumberTheory.NumberField.Units.DirichletTheorem

/-!
# Third Cyclotomic Field
We gather various results about the third cyclotomic field. The following notations are used in this
file: `K` is a number field such that `IsCyclotomicExtension {3} â„š K`, `Î¶` is any primitive `3`-rd
root of unity in `K`, `Î·` is the element in the ring of integers corresponding to `Î¶` and
`Î» = Î· - 1`.

## Main results
* `IsCyclotomicExtension.Rat.Three.Units.mem`: Given a unit `u : (ğ“ K)Ë£`, we have that
`u âˆˆ ({1, -1, Î·, -Î·, Î·^2, -Î·^2}`.

* `IsCyclotomicExtension.Rat.Three.Units.mem.eq_one_or_neg_one_of_unit_of_congruent`: Given a unit
`u : (ğ“ K)Ë£`, if `u` is congruent to an integer modulo `3`, then `u = 1` or `u = -1`.

This is a special case of the so-called *Kummer's lemma*.
-/

open NumberField Units InfinitePlace nonZeroDivisors Polynomial

namespace IsCyclotomicExtension.Rat.Three

variable {K : Type*} [Field K] [NumberField K] [IsCyclotomicExtension {3} â„š K]
variable {Î¶ : K} (hÎ¶ : IsPrimitiveRoot Î¶ â†‘(3 : â„•+)) (u : (ğ“ K)Ë£)
local notation3 "Î·" => hÎ¶.toInteger
local notation3 "Î»" => hÎ¶.toInteger - 1

/-- Let `u` be a unit in `(ğ“ K)Ë£`, then `u âˆˆ {1, -1, Î·, -Î·, Î·^2, -Î·^2}`. -/
theorem Units.mem : â†‘u âˆˆ({1, -1, Î·, -Î·, Î· ^ 2, -Î· ^ 2} : Set (ğ“ K)) := by
  have hrank : rank K = 0 := by
    dsimp [rank]
    rw [card_eq_nrRealPlaces_add_nrComplexPlaces, nrRealPlaces_eq_zero (n := 3) K (by decide),
      zero_add, nrComplexPlaces_eq_totient_div_two (n := 3)]
    rfl
  obtain âŸ¨âŸ¨x, eâŸ©, hxu, -âŸ© := exist_unique_eq_mul_prod _ u
  replace hxu : u = x := by
    rw [â† mul_one x.1, hxu]
    apply congr_arg
    rw [â† Finset.prod_empty]
    congr
    rw [Finset.univ_eq_empty_iff, hrank]
    infer_instance
  obtain âŸ¨n, hnpos, hnâŸ© := isOfFinOrder_iff_pow_eq_one.1 <| (CommGroup.mem_torsion _ _).1 x.2
  replace hn : (â†‘u : K) ^ ((âŸ¨n, hnposâŸ© : â„•+) : â„•) = 1 := by
    rw [â† map_pow]
    convert map_one (algebraMap (ğ“ K) K)
    rw_mod_cast [hxu, hn]
    simp
  have hodd : Odd ((3 : â„•+) : â„•) := by decide
  obtain âŸ¨r, hr3, hruâŸ© := hÎ¶.exists_pow_or_neg_mul_pow_of_isOfFinOrder hodd
    (isOfFinOrder_iff_pow_eq_one.2 âŸ¨n, hnpos, hnâŸ©)
  replace hr : r âˆˆ Finset.Ico 0 3 := Finset.mem_Ico.2 âŸ¨by simp, hr3âŸ©
  replace hru : â†‘u = Î· ^ r âˆ¨ â†‘u = -Î· ^ r := by
    rcases hru with (h | h)
    Â· left; ext; exact h
    Â· right; ext; exact h
  fin_cases hr
  Â· rcases hru with (h | h)
    Â· simp only [h, pow_zero, Set.mem_insert_iff, eq_neg_self_iff, one_ne_zero,
        Set.mem_singleton_iff, false_or, true_or]
    Â· simp only [h, pow_zero, Set.mem_insert_iff, neg_eq_self_iff, one_ne_zero, neg_inj,
        Set.mem_singleton_iff, true_or, or_true]
  Â· rcases hru with (h | h)
    Â· simp only [h, zero_add, pow_one, Set.mem_insert_iff, eq_neg_self_iff, Set.mem_singleton_iff,
      true_or, or_true]
    Â· simp only [h, zero_add, pow_one, Set.mem_insert_iff, neg_inj, neg_eq_self_iff,
      Set.mem_singleton_iff, true_or, or_true]
  Â· rcases hru with (h | h)
    Â· apply Set.mem_insert_of_mem; apply Set.mem_insert_of_mem; simp [h]
    Â· apply Set.mem_insert_of_mem; apply Set.mem_insert_of_mem; simp [h]

/-- Let `K` be a number field such that `IsCyclotomicExtension {3} â„š K`.
Let `Î¶` be any primitive `3`-rd root of unity in `K`.
Let `Î·` be the element in the ring of integers corresponding to `Î¶`.
Let `Î»` be the element in the ring of integers corresponding to `Î¶ - 1`.

Then `Î» ^ 2 = -3 * Î·`. -/
lemma lambda_sq : Î» ^ 2 = -3 * Î· :=
  calc Î» ^ 2 = Î· ^ 2 + Î· + 1 - 3 * Î· := by ring
  _ = 0 - 3 * Î· := by ext; simpa using hÎ¶.isRoot_cyclotomic (by decide)
  _ = -3 * Î· := by ring

/-- Let `K` be a number field such that `IsCyclotomicExtension {3} â„š K`.
Let `Î¶` be any primitive `3`-rd root of unity in `K`.
Let `Î·` be the element in the ring of integers corresponding to `Î¶`.
Let `Î»` be the element in the ring of integers corresponding to `Î¶ - 1`.
Let `u` be a unit in `(ğ“ K)Ë£`.

If `u` is congruent to an integer modulo `Î» ^ 2`, then `u = 1` or `u = -1`.

This is a special case of the so-called *Kummer's lemma*. -/
theorem eq_one_or_neg_one_of_unit_of_congruent (hcong : âˆƒ n : â„¤, Î» ^ 2 âˆ£ (u - n : ğ“ K)) :
    u = 1 âˆ¨ u = -1 := by
  replace hcong : âˆƒ n : â„¤, (3 : ğ“ K) âˆ£ (â†‘u - n : ğ“ K) := by
    obtain âŸ¨n, x, hxâŸ© := hcong
    exact âŸ¨n, -Î· * x, by rw [â† mul_assoc, mul_neg, â† neg_mul, â† lambda_sq, hx]âŸ©
  have hÎ¶ := IsCyclotomicExtension.zeta_spec 3 â„š K
  have := Units.mem hÎ¶ u
  have h2 : (hÎ¶.pow_of_coprime 2 (by decide)).toInteger = hÎ¶.toInteger ^ 2 := by ext; simp
  simp only [Set.mem_insert_iff, val_eq_one, Set.mem_singleton_iff] at this
  rcases this with (rfl | h | h | h | h | h)
  Â· left; rfl
  Â· right; ext; simp [h]
  Â· exfalso
    apply hÎ¶.not_exists_int_prime_dvd_sub_of_prime_ne_two' (by decide)
    rw [â† h]
    exact hcong
  Â· exfalso
    apply hÎ¶.not_exists_int_prime_dvd_sub_of_prime_ne_two' (by decide)
    obtain âŸ¨n, x, hxâŸ© := hcong
    rw [sub_eq_iff_eq_add] at hx
    refine âŸ¨-n, -x, ?_âŸ©
    rw [â† neg_eq_iff_eq_neg.2 h, hx]
    simp
  Â· exfalso
    apply (hÎ¶.pow_of_coprime 2 (by decide)).not_exists_int_prime_dvd_sub_of_prime_ne_two'
      (by decide)
    rw [h2, â† h]
    exact hcong
  Â· exfalso
    apply (hÎ¶.pow_of_coprime 2 (by decide)).not_exists_int_prime_dvd_sub_of_prime_ne_two'
      (by decide)
    obtain âŸ¨n, x, hxâŸ© := hcong
    refine âŸ¨-n, -x, ?_âŸ©
    simp only [Int.cast_neg, sub_neg_eq_add, PNat.val_ofNat, Nat.cast_ofNat]
    rw [h2, mul_neg, â† hx, â† neg_eq_iff_eq_neg.2 h]
    simp only [Int.cast_neg, sub_neg_eq_add, neg_sub]
    ring
