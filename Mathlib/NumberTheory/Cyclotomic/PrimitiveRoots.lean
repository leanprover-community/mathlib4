/-
Copyright (c) 2022 Riccardo Brasca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alex Best, Riccardo Brasca, Eric Rodriguez
-/
import Mathlib.Data.PNat.Prime
import Mathlib.Algebra.IsPrimePow
import Mathlib.NumberTheory.Cyclotomic.Basic
import Mathlib.RingTheory.Adjoin.PowerBasis
import Mathlib.RingTheory.Polynomial.Cyclotomic.Eval
import Mathlib.RingTheory.Norm
import Mathlib.RingTheory.Polynomial.Cyclotomic.Expand

#align_import number_theory.cyclotomic.primitive_roots from "leanprover-community/mathlib"@"5bfbcca0a7ffdd21cf1682e59106d6c942434a32"

/-!
# Primitive roots in cyclotomic fields
If `IsCyclotomicExtension {n} A B`, we define an element `zeta n A B : B` that is a primitive
`n`th-root of unity in `B` and we study its properties. We also prove related theorems under the
more general assumption of just being a primitive root, for reasons described in the implementation
details section.

## Main definitions
* `IsCyclotomicExtension.zeta n A B`: if `IsCyclotomicExtension {n} A B`, than `zeta n A B`
  is a primitive `n`-th root of unity in `B`.
* `IsPrimitiveRoot.powerBasis`: if `K` and `L` are fields such that
  `IsCyclotomicExtension {n} K L`, then `IsPrimitiveRoot.powerBasis`
  gives a `K`-power basis for `L` given a primitive root `Œ∂`.
* `IsPrimitiveRoot.embeddingsEquivPrimitiveRoots`: the equivalence between `L ‚Üí‚Çê[K] A`
  and `primitiveroots n A` given by the choice of `Œ∂`.

## Main results
* `IsCyclotomicExtension.zeta_spec`: `zeta n A B` is a primitive `n`-th root of unity.
* `IsCyclotomicExtension.finrank`: if `Irreducible (cyclotomic n K)` (in particular for
  `K = ‚Ñö`), then the `finrank` of a cyclotomic extension is `n.totient`.
* `IsPrimitiveRoot.norm_eq_one`: if `Irreducible (cyclotomic n K)` (in particular for `K = ‚Ñö`),
  the norm of a primitive root is `1` if `n ‚â† 2`.
* `IsPrimitiveRoot.sub_one_norm_eq_eval_cyclotomic`: if `Irreducible (cyclotomic n K)`
  (in particular for `K = ‚Ñö`), then the norm of `Œ∂ - 1` is `eval 1 (cyclotomic n ‚Ñ§)`, for a
  primitive root `Œ∂`. We also prove the analogous of this result for `zeta`.
* `IsPrimitiveRoot.pow_sub_one_norm_prime_pow_ne_two` : if
  `Irreducible (cyclotomic (p ^ (k + 1)) K)` (in particular for `K = ‚Ñö`) and `p` is a prime,
  then the norm of `Œ∂ ^ (p ^ s) - 1` is `p ^ (p ^ s)` `p ^ (k - s + 1) ‚â† 2`. See the following
  lemmas for similar results. We also prove the analogous of this result for `zeta`.
* `IsPrimitiveRoot.sub_one_norm_prime_ne_two` : if `Irreducible (cyclotomic (p ^ (k + 1)) K)`
  (in particular for `K = ‚Ñö`) and `p` is an odd prime, then the norm of `Œ∂ - 1` is `p`. We also
  prove the analogous of this result for `zeta`.
* `IsPrimitiveRoot.embeddingsEquivPrimitiveRoots`: the equivalence between `L ‚Üí‚Çê[K] A`
  and `primitiveRoots n A` given by the choice of `Œ∂`.

## Implementation details
`zeta n A B` is defined as any primitive root of unity in `B`, - this must exist, by definition of
`IsCyclotomicExtension`. It is not true in general that it is a root of `cyclotomic n B`,
but this holds if `isDomain B` and `NeZero (‚Üën : B)`.

`zeta n A B` is defined using `Exists.choose`, which means we cannot control it.
For example, in normal mathematics, we can demand that `(zeta p ‚Ñ§ ‚Ñ§[Œ∂‚Çö] : ‚Ñö(Œ∂‚Çö))` is equal to
`zeta p ‚Ñö ‚Ñö(Œ∂‚Çö)`, as we are just choosing "an arbitrary primitive root" and we can internally
specify that our choices agree. This is not the case here, and it is indeed impossible to prove that
these two are equal. Therefore, whenever possible, we prove our results for any primitive root,
and only at the "final step", when we need to provide an "explicit" primitive root, we use `zeta`.

-/


open Polynomial Algebra Finset FiniteDimensional IsCyclotomicExtension Nat PNat Set

universe u v w z

variable {p n : ‚Ñï+} (A : Type w) (B : Type z) (K : Type u) {L : Type v} (C : Type w)

variable [CommRing A] [CommRing B] [Algebra A B] [IsCyclotomicExtension {n} A B]

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220
section Zeta

namespace IsCyclotomicExtension

variable (n)

/-- If `B` is an `n`-th cyclotomic extension of `A`, then `zeta n A B` is a primitive root of
unity in `B`. -/
noncomputable def zeta : B :=
  (exists_prim_root A <| Set.mem_singleton n : ‚àÉ r : B, IsPrimitiveRoot r n).choose
#align is_cyclotomic_extension.zeta IsCyclotomicExtension.zeta

/-- `zeta n A B` is a primitive `n`-th root of unity. -/
@[simp]
theorem zeta_spec : IsPrimitiveRoot (zeta n A B) n :=
  Classical.choose_spec (exists_prim_root A (Set.mem_singleton n) : ‚àÉ r : B, IsPrimitiveRoot r n)
#align is_cyclotomic_extension.zeta_spec IsCyclotomicExtension.zeta_spec

theorem aeval_zeta [IsDomain B] [NeZero ((n : ‚Ñï) : B)] :
    aeval (zeta n A B) (cyclotomic n A) = 0 := by
  rw [aeval_def, ‚Üê eval_map, ‚Üê IsRoot.def, map_cyclotomic, isRoot_cyclotomic_iff]
  -- ‚ä¢ IsPrimitiveRoot (zeta n A B) ‚Üën
  exact zeta_spec n A B
  -- üéâ no goals
#align is_cyclotomic_extension.aeval_zeta IsCyclotomicExtension.aeval_zeta

theorem zeta_isRoot [IsDomain B] [NeZero ((n : ‚Ñï) : B)] : IsRoot (cyclotomic n B) (zeta n A B) := by
  convert aeval_zeta n A B using 0
  -- ‚ä¢ IsRoot (cyclotomic (‚Üën) B) (zeta n A B) ‚Üî ‚Üë(aeval (zeta n A B)) (cyclotomic  ‚Ä¶
  rw [IsRoot.def, aeval_def, eval‚ÇÇ_eq_eval_map, map_cyclotomic]
  -- üéâ no goals
#align is_cyclotomic_extension.zeta_is_root IsCyclotomicExtension.zeta_isRoot

theorem zeta_pow : zeta n A B ^ (n : ‚Ñï) = 1 :=
  (zeta_spec n A B).pow_eq_one
#align is_cyclotomic_extension.zeta_pow IsCyclotomicExtension.zeta_pow

end IsCyclotomicExtension

end Zeta

section NoOrder

variable [Field K] [CommRing L] [IsDomain L] [Algebra K L] [IsCyclotomicExtension {n} K L] {Œ∂ : L}
  (hŒ∂ : IsPrimitiveRoot Œ∂ n)

namespace IsPrimitiveRoot

variable {C}

/-- The `PowerBasis` given by a primitive root `Œ∑`. -/
@[simps!]
protected noncomputable def powerBasis : PowerBasis K L :=
  PowerBasis.map (Algebra.adjoin.powerBasis <| integral {n} K L Œ∂) <|
    (Subalgebra.equivOfEq _ _ (IsCyclotomicExtension.adjoin_primitive_root_eq_top hŒ∂)).trans
      Subalgebra.topEquiv
#align is_primitive_root.power_basis IsPrimitiveRoot.powerBasis

theorem powerBasis_gen_mem_adjoin_zeta_sub_one :
    (hŒ∂.powerBasis K).gen ‚àà adjoin K ({Œ∂ - 1} : Set L) := by
  rw [powerBasis_gen, adjoin_singleton_eq_range_aeval, AlgHom.mem_range]
  -- ‚ä¢ ‚àÉ x, ‚Üë(aeval (Œ∂ - 1)) x = Œ∂
  exact ‚ü®X + 1, by simp‚ü©
  -- üéâ no goals
#align is_primitive_root.power_basis_gen_mem_adjoin_zeta_sub_one IsPrimitiveRoot.powerBasis_gen_mem_adjoin_zeta_sub_one

/-- The `PowerBasis` given by `Œ∑ - 1`. -/
@[simps!]
noncomputable def subOnePowerBasis : PowerBasis K L :=
  (hŒ∂.powerBasis K).ofGenMemAdjoin
    (isIntegral_sub (IsCyclotomicExtension.integral {n} K L Œ∂) isIntegral_one)
    (hŒ∂.powerBasis_gen_mem_adjoin_zeta_sub_one _)
#align is_primitive_root.sub_one_power_basis IsPrimitiveRoot.subOnePowerBasis

variable {K} (C)

-- We are not using @[simps] to avoid a timeout.
/-- The equivalence between `L ‚Üí‚Çê[K] C` and `primitiveRoots n C` given by a primitive root `Œ∂`. -/
noncomputable def embeddingsEquivPrimitiveRoots (C : Type*) [CommRing C] [IsDomain C] [Algebra K C]
    (hirr : Irreducible (cyclotomic n K)) : (L ‚Üí‚Çê[K] C) ‚âÉ primitiveRoots n C :=
  (hŒ∂.powerBasis K).liftEquiv.trans
    { toFun := fun x => by
        haveI := IsCyclotomicExtension.neZero' n K L
        -- ‚ä¢ { x // x ‚àà primitiveRoots (‚Üën) C }
        haveI hn := NeZero.of_noZeroSMulDivisors K C n
        -- ‚ä¢ { x // x ‚àà primitiveRoots (‚Üën) C }
        refine' ‚ü®x.1, _‚ü©
        -- ‚ä¢ ‚Üëx ‚àà primitiveRoots (‚Üën) C
        cases x
        -- ‚ä¢ ‚Üë{ val := val‚úù, property := property‚úù } ‚àà primitiveRoots (‚Üën) C
        rwa [mem_primitiveRoots n.pos, ‚Üê isRoot_cyclotomic_iff, IsRoot.def, ‚Üê
          map_cyclotomic _ (algebraMap K C), hŒ∂.minpoly_eq_cyclotomic_of_irreducible hirr, ‚Üê
          eval‚ÇÇ_eq_eval_map, ‚Üê aeval_def]
      invFun := fun x => by
        haveI := IsCyclotomicExtension.neZero' n K L
        -- ‚ä¢ { y // ‚Üë(aeval y) (minpoly K (IsPrimitiveRoot.powerBasis K hŒ∂).gen) = 0 }
        haveI hn := NeZero.of_noZeroSMulDivisors K C n
        -- ‚ä¢ { y // ‚Üë(aeval y) (minpoly K (IsPrimitiveRoot.powerBasis K hŒ∂).gen) = 0 }
        refine' ‚ü®x.1, _‚ü©
        -- ‚ä¢ ‚Üë(aeval ‚Üëx) (minpoly K (IsPrimitiveRoot.powerBasis K hŒ∂).gen) = 0
        cases x
        -- ‚ä¢ ‚Üë(aeval ‚Üë{ val := val‚úù, property := property‚úù }) (minpoly K (IsPrimitiveRoot ‚Ä¶
        rwa [aeval_def, eval‚ÇÇ_eq_eval_map, hŒ∂.powerBasis_gen K, ‚Üê
          hŒ∂.minpoly_eq_cyclotomic_of_irreducible hirr, map_cyclotomic, ‚Üê IsRoot.def,
          isRoot_cyclotomic_iff, ‚Üê mem_primitiveRoots n.pos]
      left_inv := fun x => Subtype.ext rfl
      right_inv := fun x => Subtype.ext rfl }
#align is_primitive_root.embeddings_equiv_primitive_roots IsPrimitiveRoot.embeddingsEquivPrimitiveRoots

-- Porting note: renamed argument `œÜ`: "expected '_' or identifier"
@[simp]
theorem embeddingsEquivPrimitiveRoots_apply_coe (C : Type*) [CommRing C] [IsDomain C] [Algebra K C]
    (hirr : Irreducible (cyclotomic n K)) (œÜ' : L ‚Üí‚Çê[K] C) :
    (hŒ∂.embeddingsEquivPrimitiveRoots C hirr œÜ' : C) = œÜ' Œ∂ :=
  rfl
#align is_primitive_root.embeddings_equiv_primitive_roots_apply_coe IsPrimitiveRoot.embeddingsEquivPrimitiveRoots_apply_coe

end IsPrimitiveRoot

namespace IsCyclotomicExtension

variable {K} (L)

/-- If `Irreducible (cyclotomic n K)` (in particular for `K = ‚Ñö`), then the `finrank` of a
cyclotomic extension is `n.totient`. -/
theorem finrank (hirr : Irreducible (cyclotomic n K)) : finrank K L = (n : ‚Ñï).totient := by
  haveI := IsCyclotomicExtension.neZero' n K L
  -- ‚ä¢ FiniteDimensional.finrank K L = œÜ ‚Üën
  rw [((zeta_spec n K L).powerBasis K).finrank, IsPrimitiveRoot.powerBasis_dim, ‚Üê
    (zeta_spec n K L).minpoly_eq_cyclotomic_of_irreducible hirr, natDegree_cyclotomic]
#align is_cyclotomic_extension.finrank IsCyclotomicExtension.finrank

end IsCyclotomicExtension

end NoOrder

section Norm

namespace IsPrimitiveRoot

section CommRing

variable [CommRing L] {Œ∂ : L} (hŒ∂ : IsPrimitiveRoot Œ∂ n)

variable {K} [Field K] [Algebra K L]

/-- This mathematically trivial result is complementary to `norm_eq_one` below. -/
theorem norm_eq_neg_one_pow (hŒ∂ : IsPrimitiveRoot Œ∂ 2) [IsDomain L] :
    norm K Œ∂ = (-1 : K) ^ finrank K L := by
  rw [hŒ∂.eq_neg_one_of_two_right, show -1 = algebraMap K L (-1) by simp, Algebra.norm_algebraMap]
  -- üéâ no goals
#align is_primitive_root.norm_eq_neg_one_pow IsPrimitiveRoot.norm_eq_neg_one_pow

/-- If `Irreducible (cyclotomic n K)` (in particular for `K = ‚Ñö`), the norm of a primitive root is
`1` if `n ‚â† 2`. -/
theorem norm_eq_one [IsDomain L] [IsCyclotomicExtension {n} K L] (hn : n ‚â† 2)
    (hirr : Irreducible (cyclotomic n K)) : norm K Œ∂ = 1 := by
  haveI := IsCyclotomicExtension.neZero' n K L
  -- ‚ä¢ ‚Üë(Algebra.norm K) Œ∂ = 1
  by_cases h1 : n = 1
  -- ‚ä¢ ‚Üë(Algebra.norm K) Œ∂ = 1
  ¬∑ rw [h1, one_coe, one_right_iff] at hŒ∂
    -- ‚ä¢ ‚Üë(Algebra.norm K) Œ∂ = 1
    rw [hŒ∂, show 1 = algebraMap K L 1 by simp, Algebra.norm_algebraMap, one_pow]
    -- üéâ no goals
  ¬∑ replace h1 : 2 ‚â§ n
    -- ‚ä¢ 2 ‚â§ n
    ¬∑ by_contra' h
      -- ‚ä¢ False
      exact h1 (PNat.eq_one_of_lt_two h)
      -- üéâ no goals
-- Porting note: specyfing the type of `cyclotomic_coeff_zero K h1` was not needed.
    rw [‚Üê hŒ∂.powerBasis_gen K, PowerBasis.norm_gen_eq_coeff_zero_minpoly, hŒ∂.powerBasis_gen K, ‚Üê
      hŒ∂.minpoly_eq_cyclotomic_of_irreducible hirr,
      (cyclotomic_coeff_zero K h1 : coeff (cyclotomic n K) 0 = 1), mul_one,
      hŒ∂.powerBasis_dim K, ‚Üê hŒ∂.minpoly_eq_cyclotomic_of_irreducible hirr, natDegree_cyclotomic]
    exact (totient_even <| h1.lt_of_ne hn.symm).neg_one_pow
    -- üéâ no goals
#align is_primitive_root.norm_eq_one IsPrimitiveRoot.norm_eq_one

/-- If `K` is linearly ordered, the norm of a primitive root is `1` if `n` is odd. -/
theorem norm_eq_one_of_linearly_ordered {K : Type*} [LinearOrderedField K] [Algebra K L]
    (hodd : Odd (n : ‚Ñï)) : norm K Œ∂ = 1 := by
  have hz := congr_arg (norm K) ((IsPrimitiveRoot.iff_def _ n).1 hŒ∂).1
  -- ‚ä¢ ‚Üë(Algebra.norm K) Œ∂ = 1
  rw [‚Üê (algebraMap K L).map_one, Algebra.norm_algebraMap, one_pow, map_pow, ‚Üê one_pow ‚Üën] at hz
  -- ‚ä¢ ‚Üë(Algebra.norm K) Œ∂ = 1
  exact StrictMono.injective hodd.strictMono_pow hz
  -- üéâ no goals
#align is_primitive_root.norm_eq_one_of_linearly_ordered IsPrimitiveRoot.norm_eq_one_of_linearly_ordered

theorem norm_of_cyclotomic_irreducible [IsDomain L] [IsCyclotomicExtension {n} K L]
    (hirr : Irreducible (cyclotomic n K)) : norm K Œ∂ = ite (n = 2) (-1) 1 := by
  split_ifs with hn
  -- ‚ä¢ ‚Üë(Algebra.norm K) Œ∂ = -1
  ¬∑ subst hn
    -- ‚ä¢ ‚Üë(Algebra.norm K) Œ∂ = -1
    convert norm_eq_neg_one_pow (K := K) hŒ∂
    -- ‚ä¢ -1 = (-1) ^ FiniteDimensional.finrank K L
    erw [IsCyclotomicExtension.finrank _ hirr, totient_two, pow_one]
    -- üéâ no goals
  ¬∑ exact hŒ∂.norm_eq_one hn hirr
    -- üéâ no goals
#align is_primitive_root.norm_of_cyclotomic_irreducible IsPrimitiveRoot.norm_of_cyclotomic_irreducible

end CommRing

section Field

variable [Field L] {Œ∂ : L} (hŒ∂ : IsPrimitiveRoot Œ∂ n)

variable {K} [Field K] [Algebra K L]

/-- If `Irreducible (cyclotomic n K)` (in particular for `K = ‚Ñö`), then the norm of
`Œ∂ - 1` is `eval 1 (cyclotomic n ‚Ñ§)`. -/
theorem sub_one_norm_eq_eval_cyclotomic [IsCyclotomicExtension {n} K L] (h : 2 < (n : ‚Ñï))
    (hirr : Irreducible (cyclotomic n K)) : norm K (Œ∂ - 1) = ‚Üë(eval 1 (cyclotomic n ‚Ñ§)) := by
  haveI := IsCyclotomicExtension.neZero' n K L
  -- ‚ä¢ ‚Üë(Algebra.norm K) (Œ∂ - 1) = ‚Üë(eval 1 (cyclotomic ‚Üën ‚Ñ§))
  let E := AlgebraicClosure L
  -- ‚ä¢ ‚Üë(Algebra.norm K) (Œ∂ - 1) = ‚Üë(eval 1 (cyclotomic ‚Üën ‚Ñ§))
  obtain ‚ü®z, hz‚ü© := IsAlgClosed.exists_root _ (degree_cyclotomic_pos n E n.pos).ne.symm
  -- ‚ä¢ ‚Üë(Algebra.norm K) (Œ∂ - 1) = ‚Üë(eval 1 (cyclotomic ‚Üën ‚Ñ§))
  apply (algebraMap K E).injective
  -- ‚ä¢ ‚Üë(algebraMap K E) (‚Üë(Algebra.norm K) (Œ∂ - 1)) = ‚Üë(algebraMap K E) ‚Üë(eval 1 ( ‚Ä¶
  letI := IsCyclotomicExtension.finiteDimensional {n} K L
  -- ‚ä¢ ‚Üë(algebraMap K E) (‚Üë(Algebra.norm K) (Œ∂ - 1)) = ‚Üë(algebraMap K E) ‚Üë(eval 1 ( ‚Ä¶
  letI := IsCyclotomicExtension.isGalois n K L
  -- ‚ä¢ ‚Üë(algebraMap K E) (‚Üë(Algebra.norm K) (Œ∂ - 1)) = ‚Üë(algebraMap K E) ‚Üë(eval 1 ( ‚Ä¶
  rw [norm_eq_prod_embeddings]
  -- ‚ä¢ (Finset.prod Finset.univ fun œÉ => ‚ÜëœÉ (Œ∂ - 1)) = ‚Üë(algebraMap K E) ‚Üë(eval 1 ( ‚Ä¶
  conv_lhs =>
    congr
    rfl
    ext
    rw [‚Üê neg_sub, AlgHom.map_neg, AlgHom.map_sub, AlgHom.map_one, neg_eq_neg_one_mul]
  rw [prod_mul_distrib, prod_const, card_univ, AlgHom.card, IsCyclotomicExtension.finrank L hirr,
    (totient_even h).neg_one_pow, one_mul]
  have Hprod : (Finset.univ.prod fun œÉ : L ‚Üí‚Çê[K] E => 1 - œÉ Œ∂) = eval 1 (cyclotomic' n E) := by
    rw [cyclotomic', eval_prod, ‚Üê @Finset.prod_attach E E, ‚Üê univ_eq_attach]
    refine' Fintype.prod_equiv (hŒ∂.embeddingsEquivPrimitiveRoots E hirr) _ _ fun œÉ => _
    simp
  haveI : NeZero ((n : ‚Ñï) : E) := NeZero.of_noZeroSMulDivisors K _ (n : ‚Ñï)
  -- ‚ä¢ (Finset.prod Finset.univ fun x => 1 - ‚Üëx Œ∂) = ‚Üë(algebraMap K E) ‚Üë(eval 1 (cy ‚Ä¶
  rw [Hprod, cyclotomic', ‚Üê cyclotomic_eq_prod_X_sub_primitiveRoots (isRoot_cyclotomic_iff.1 hz),
    ‚Üê map_cyclotomic_int, _root_.map_intCast, ‚Üê Int.cast_one, eval_int_cast_map, eq_intCast,
    Int.cast_id]
#align is_primitive_root.sub_one_norm_eq_eval_cyclotomic IsPrimitiveRoot.sub_one_norm_eq_eval_cyclotomic

/-- If `IsPrimePow (n : ‚Ñï)`, `n ‚â† 2` and `Irreducible (cyclotomic n K)` (in particular for
`K = ‚Ñö`), then the norm of `Œ∂ - 1` is `(n : ‚Ñï).minFac`. -/
theorem sub_one_norm_isPrimePow (hn : IsPrimePow (n : ‚Ñï)) [IsCyclotomicExtension {n} K L]
    (hirr : Irreducible (cyclotomic (n : ‚Ñï) K)) (h : n ‚â† 2) : norm K (Œ∂ - 1) = (n : ‚Ñï).minFac := by
  have :=
    (coe_lt_coe 2 _).1
      (lt_of_le_of_ne (succ_le_of_lt (IsPrimePow.one_lt hn))
        (Function.Injective.ne PNat.coe_injective h).symm)
  letI hprime : Fact (n : ‚Ñï).minFac.Prime := ‚ü®minFac_prime (IsPrimePow.ne_one hn)‚ü©
  -- ‚ä¢ ‚Üë(Algebra.norm K) (Œ∂ - 1) = ‚Üë(minFac ‚Üën)
  rw [sub_one_norm_eq_eval_cyclotomic hŒ∂ this hirr]
  -- ‚ä¢ ‚Üë(eval 1 (cyclotomic ‚Üën ‚Ñ§)) = ‚Üë(minFac ‚Üën)
  nth_rw 1 [‚Üê IsPrimePow.minFac_pow_factorization_eq hn]
  -- ‚ä¢ ‚Üë(eval 1 (cyclotomic (minFac ‚Üën ^ ‚Üë(Nat.factorization ‚Üën) (minFac ‚Üën)) ‚Ñ§)) = ‚Ä¶
  obtain ‚ü®k, hk‚ü© : ‚àÉ k, (n : ‚Ñï).factorization (n : ‚Ñï).minFac = k + 1 :=
    exists_eq_succ_of_ne_zero
      (((n : ‚Ñï).factorization.mem_support_toFun (n : ‚Ñï).minFac).1 <|
        factor_iff_mem_factorization.2 <|
          (mem_factors (IsPrimePow.ne_zero hn)).2 ‚ü®hprime.out, minFac_dvd _‚ü©)
  simp [hk, sub_one_norm_eq_eval_cyclotomic hŒ∂ this hirr]
  -- üéâ no goals
#align is_primitive_root.sub_one_norm_is_prime_pow IsPrimitiveRoot.sub_one_norm_isPrimePow

variable {A}

theorem minpoly_sub_one_eq_cyclotomic_comp [Algebra K A] [IsDomain A] {Œ∂ : A}
    [IsCyclotomicExtension {n} K A] (hŒ∂ : IsPrimitiveRoot Œ∂ n)
    (h : Irreducible (Polynomial.cyclotomic n K)) :
    minpoly K (Œ∂ - 1) = (cyclotomic n K).comp (X + 1) := by
  haveI := IsCyclotomicExtension.neZero' n K A
  -- ‚ä¢ minpoly K (Œ∂ - 1) = comp (cyclotomic (‚Üën) K) (X + 1)
  rw [show Œ∂ - 1 = Œ∂ + algebraMap K A (-1) by simp [sub_eq_add_neg],
    minpoly.add_algebraMap (IsCyclotomicExtension.integral {n} K A Œ∂),
    hŒ∂.minpoly_eq_cyclotomic_of_irreducible h]
  simp
  -- üéâ no goals
#align is_primitive_root.minpoly_sub_one_eq_cyclotomic_comp IsPrimitiveRoot.minpoly_sub_one_eq_cyclotomic_comp

open scoped Cyclotomic

/-- If `Irreducible (cyclotomic (p ^ (k + 1)) K)` (in particular for `K = ‚Ñö`) and `p` is a prime,
then the norm of `Œ∂ ^ (p ^ s) - 1` is `p ^ (p ^ s)` if `p ^ (k - s + 1) ‚â† 2`. See the next lemmas
for similar results. -/
theorem pow_sub_one_norm_prime_pow_ne_two {k s : ‚Ñï} (hŒ∂ : IsPrimitiveRoot Œ∂ ‚Üë(p ^ (k + 1)))
    [hpri : Fact (p : ‚Ñï).Prime] [IsCyclotomicExtension {p ^ (k + 1)} K L]
    (hirr : Irreducible (cyclotomic (‚Üë(p ^ (k + 1)) : ‚Ñï) K)) (hs : s ‚â§ k)
    (htwo : p ^ (k - s + 1) ‚â† 2) : norm K (Œ∂ ^ (p : ‚Ñï) ^ s - 1) = (p : K) ^ (p : ‚Ñï) ^ s := by
-- Porting note: `by simp` was `by linarith` that now fails.
  have hirr‚ÇÅ : Irreducible (cyclotomic ((p : ‚Ñï) ^ (k - s + 1)) K) :=
    cyclotomic_irreducible_pow_of_irreducible_pow hpri.1 (by simp) hirr
  rw [‚Üê PNat.pow_coe] at hirr‚ÇÅ
  -- ‚ä¢ ‚Üë(Algebra.norm K) (Œ∂ ^ ‚Üëp ^ s - 1) = ‚Üë‚Üëp ^ ‚Üëp ^ s
  set Œ∑ := Œ∂ ^ (p : ‚Ñï) ^ s - 1 with Œ∑def
  -- ‚ä¢ ‚Üë(Algebra.norm K) Œ∑ = ‚Üë‚Üëp ^ ‚Üëp ^ s
  let Œ∑‚ÇÅ : K‚üÆŒ∑‚üØ := IntermediateField.AdjoinSimple.gen K Œ∑
  -- ‚ä¢ ‚Üë(Algebra.norm K) Œ∑ = ‚Üë‚Üëp ^ ‚Üëp ^ s
  have hŒ∑ : IsPrimitiveRoot (Œ∑ + 1) ((p : ‚Ñï) ^ (k + 1 - s)) := by
    rw [sub_add_cancel]
    refine' IsPrimitiveRoot.pow (p ^ (k + 1)).pos hŒ∂ _
    rw [PNat.pow_coe, ‚Üê pow_add, add_comm s, Nat.sub_add_cancel (le_trans hs (Nat.le_succ k))]
  have : IsCyclotomicExtension {p ^ (k - s + 1)} K K‚üÆŒ∑‚üØ := by
    suffices IsCyclotomicExtension {p ^ (k - s + 1)} K K‚üÆŒ∑ + 1‚üØ.toSubalgebra by
      have H : K‚üÆŒ∑ + 1‚üØ.toSubalgebra = K‚üÆŒ∑‚üØ.toSubalgebra := by
        simp only [IntermediateField.adjoin_simple_toSubalgebra_of_integral
            (IsCyclotomicExtension.integral {p ^ (k + 1)} K L _)]
        refine' Subalgebra.ext fun x => ‚ü®fun hx => adjoin_le _ hx, fun hx => adjoin_le _ hx‚ü©
        ¬∑ simp only [Set.singleton_subset_iff, SetLike.mem_coe]
          exact Subalgebra.add_mem _ (subset_adjoin (mem_singleton Œ∑)) (Subalgebra.one_mem _)
-- Porting note `Œ∑def` was not needed.
        ¬∑ simp only [Set.singleton_subset_iff, SetLike.mem_coe, ‚Üê Œ∑def]
          nth_rw 1 [‚Üê add_sub_cancel Œ∑ 1]
          refine' Subalgebra.sub_mem _ (subset_adjoin (mem_singleton _)) (Subalgebra.one_mem _)
-- Porting note: the previous proof was `rw [H] at this; exact this` but it now fails.
      exact IsCyclotomicExtension.equiv _ _ _ (Subalgebra.equivOfEq _ _ H)
-- Porting note: the next `refine` was `rw [H]`, abusing defeq, and it now fails.
    have H := IntermediateField.adjoin_simple_toSubalgebra_of_integral
        (IsCyclotomicExtension.integral {p ^ (k + 1)} K L (Œ∑ + 1))
    refine @IsCyclotomicExtension.equiv _ _ _ _ _ _ _ _ _ ?_ (Subalgebra.equivOfEq _ _ H).symm
    have hŒ∑' : IsPrimitiveRoot (Œ∑ + 1) ‚Üë(p ^ (k + 1 - s)) := by simpa using hŒ∑
-- Porting note: `using 1` was not needed.
    convert hŒ∑'.adjoin_isCyclotomicExtension K using 1
    rw [Nat.sub_add_comm hs]
  replace hŒ∑ : IsPrimitiveRoot (Œ∑‚ÇÅ + 1) ‚Üë(p ^ (k - s + 1))
  -- ‚ä¢ IsPrimitiveRoot (Œ∑‚ÇÅ + 1) ‚Üë(p ^ (k - s + 1))
  ¬∑ apply coe_submonoidClass_iff.1
    -- ‚ä¢ IsPrimitiveRoot ‚Üë(Œ∑‚ÇÅ + 1) ‚Üë(p ^ (k - s + 1))
    convert hŒ∑ using 1
    -- ‚ä¢ ‚Üë(p ^ (k - s + 1)) = ‚Üëp ^ (k + 1 - s)
    rw [Nat.sub_add_comm hs, pow_coe]
    -- üéâ no goals
-- Porting note: the following `haveI` were not needed because the locale `cyclotomic` set them
-- as instances.
  haveI := IsCyclotomicExtension.finiteDimensional {p ^ (k + 1)} K L
  -- ‚ä¢ ‚Üë(Algebra.norm K) Œ∑ = ‚Üë‚Üëp ^ ‚Üëp ^ s
  haveI := IsCyclotomicExtension.isGalois (p ^ (k + 1)) K L
  -- ‚ä¢ ‚Üë(Algebra.norm K) Œ∑ = ‚Üë‚Üëp ^ ‚Üëp ^ s
  rw [norm_eq_norm_adjoin K]
  -- ‚ä¢ ‚Üë(Algebra.norm K) (IntermediateField.AdjoinSimple.gen K Œ∑) ^ FiniteDimension ‚Ä¶
  ¬∑ have H := hŒ∑.sub_one_norm_isPrimePow ?_ hirr‚ÇÅ htwo
    -- ‚ä¢ ‚Üë(Algebra.norm K) (IntermediateField.AdjoinSimple.gen K Œ∑) ^ FiniteDimension ‚Ä¶
    swap; ¬∑ rw [PNat.pow_coe]; exact hpri.1.isPrimePow.pow (Nat.succ_ne_zero _)
    -- ‚ä¢ IsPrimePow ‚Üë(p ^ (k - s + 1))
            -- ‚ä¢ IsPrimePow (‚Üëp ^ (k - s + 1))
                               -- üéâ no goals
    rw [add_sub_cancel] at H
    -- ‚ä¢ ‚Üë(Algebra.norm K) (IntermediateField.AdjoinSimple.gen K Œ∑) ^ FiniteDimension ‚Ä¶
    rw [H]
    -- ‚ä¢ ‚Üë(minFac ‚Üë(p ^ (k - s + 1))) ^ FiniteDimensional.finrank { x // x ‚àà K‚üÆŒ∑‚üØ } L ‚Ä¶
    congr
    -- ‚ä¢ minFac ‚Üë(p ^ (k - s + 1)) = ‚Üëp
    ¬∑ rw [PNat.pow_coe, Nat.pow_minFac, hpri.1.minFac_eq]
      -- ‚ä¢ k - s + 1 ‚â† 0
      exact Nat.succ_ne_zero _
      -- üéâ no goals
    have := FiniteDimensional.finrank_mul_finrank K K‚üÆŒ∑‚üØ L
    -- ‚ä¢ FiniteDimensional.finrank { x // x ‚àà K‚üÆŒ∑‚üØ } L = ‚Üëp ^ s
    rw [IsCyclotomicExtension.finrank L hirr, IsCyclotomicExtension.finrank K‚üÆŒ∑‚üØ hirr‚ÇÅ,
      PNat.pow_coe, PNat.pow_coe, Nat.totient_prime_pow hpri.out (k - s).succ_pos,
      Nat.totient_prime_pow hpri.out k.succ_pos, mul_comm _ ((p : ‚Ñï) - 1), mul_assoc,
      mul_comm ((p : ‚Ñï) ^ (k.succ - 1))] at this
    replace this := mul_left_cancel‚ÇÄ (tsub_pos_iff_lt.2 hpri.out.one_lt).ne' this
    -- ‚ä¢ FiniteDimensional.finrank { x // x ‚àà K‚üÆŒ∑‚üØ } L = ‚Üëp ^ s
    have Hex : k.succ - 1 = (k - s).succ - 1 + s := by
      simp only [Nat.succ_sub_succ_eq_sub, tsub_zero]
      exact (Nat.sub_add_cancel hs).symm
    rw [Hex, pow_add] at this
    -- ‚ä¢ FiniteDimensional.finrank { x // x ‚àà K‚üÆŒ∑‚üØ } L = ‚Üëp ^ s
    exact mul_left_cancel‚ÇÄ (pow_ne_zero _ hpri.out.ne_zero) this
    -- üéâ no goals
#align is_primitive_root.pow_sub_one_norm_prime_pow_ne_two IsPrimitiveRoot.pow_sub_one_norm_prime_pow_ne_two

/-- If `Irreducible (cyclotomic (p ^ (k + 1)) K)` (in particular for `K = ‚Ñö`) and `p` is a prime,
then the norm of `Œ∂ ^ (p ^ s) - 1` is `p ^ (p ^ s)` if `p ‚â† 2`. -/
theorem pow_sub_one_norm_prime_ne_two {k : ‚Ñï} (hŒ∂ : IsPrimitiveRoot Œ∂ ‚Üë(p ^ (k + 1)))
    [hpri : Fact (p : ‚Ñï).Prime] [IsCyclotomicExtension {p ^ (k + 1)} K L]
    (hirr : Irreducible (cyclotomic (‚Üë(p ^ (k + 1)) : ‚Ñï) K)) {s : ‚Ñï} (hs : s ‚â§ k) (hodd : p ‚â† 2) :
    norm K (Œ∂ ^ (p : ‚Ñï) ^ s - 1) = (p : K) ^ (p : ‚Ñï) ^ s := by
  refine' hŒ∂.pow_sub_one_norm_prime_pow_ne_two hirr hs fun h => _
  -- ‚ä¢ False
  have coe_two : ((2 : ‚Ñï+) : ‚Ñï) = 2 := by norm_cast
  -- ‚ä¢ False
  rw [‚Üê PNat.coe_inj, coe_two, PNat.pow_coe, ‚Üê pow_one 2] at h
  -- ‚ä¢ False
-- Porting note: the proof is slightly different because of coercions.
  replace h :=
    eq_of_prime_pow_eq (prime_iff.1 hpri.out) (prime_iff.1 Nat.prime_two) (k - s).succ_pos h
  exact hodd (PNat.coe_injective h)
  -- üéâ no goals

#align is_primitive_root.pow_sub_one_norm_prime_ne_two IsPrimitiveRoot.pow_sub_one_norm_prime_ne_two

/-- If `Irreducible (cyclotomic (p ^ (k + 1)) K)` (in particular for `K = ‚Ñö`) and `p` is an odd
prime, then the norm of `Œ∂ - 1` is `p`. -/
theorem sub_one_norm_prime_ne_two {k : ‚Ñï} (hŒ∂ : IsPrimitiveRoot Œ∂ ‚Üë(p ^ (k + 1)))
    [hpri : Fact (p : ‚Ñï).Prime] [IsCyclotomicExtension {p ^ (k + 1)} K L]
    (hirr : Irreducible (cyclotomic (‚Üë(p ^ (k + 1)) : ‚Ñï) K)) (h : p ‚â† 2) : norm K (Œ∂ - 1) = p := by
  simpa using hŒ∂.pow_sub_one_norm_prime_ne_two hirr k.zero_le h
  -- üéâ no goals
#align is_primitive_root.sub_one_norm_prime_ne_two IsPrimitiveRoot.sub_one_norm_prime_ne_two

/-- If `Irreducible (cyclotomic p K)` (in particular for `K = ‚Ñö`) and `p` is an odd prime,
then the norm of `Œ∂ - 1` is `p`. -/
theorem sub_one_norm_prime [hpri : Fact (p : ‚Ñï).Prime] [hcyc : IsCyclotomicExtension {p} K L]
    (hŒ∂ : IsPrimitiveRoot Œ∂ p) (hirr : Irreducible (cyclotomic p K)) (h : p ‚â† 2) :
    norm K (Œ∂ - 1) = p := by
  replace hirr : Irreducible (cyclotomic (‚Üë(p ^ (0 + 1)) : ‚Ñï) K) := by simp [hirr]
  -- ‚ä¢ ‚Üë(Algebra.norm K) (Œ∂ - 1) = ‚Üë‚Üëp
  replace hŒ∂ : IsPrimitiveRoot Œ∂ (‚Üë(p ^ (0 + 1)) : ‚Ñï) := by simp [hŒ∂]
  -- ‚ä¢ ‚Üë(Algebra.norm K) (Œ∂ - 1) = ‚Üë‚Üëp
  haveI : IsCyclotomicExtension {p ^ (0 + 1)} K L := by simp [hcyc]
  -- ‚ä¢ ‚Üë(Algebra.norm K) (Œ∂ - 1) = ‚Üë‚Üëp
  simpa using sub_one_norm_prime_ne_two hŒ∂ hirr h
  -- üéâ no goals
#align is_primitive_root.sub_one_norm_prime IsPrimitiveRoot.sub_one_norm_prime

/-- If `Irreducible (cyclotomic (2 ^ (k + 1)) K)` (in particular for `K = ‚Ñö`), then the norm of
`Œ∂ ^ (2 ^ k) - 1` is `(-2) ^ (2 ^ k)`. -/
-- Porting note: writing `(2 : ‚Ñï+)` was not needed (similarly everywhere).
theorem pow_sub_one_norm_two {k : ‚Ñï} (hŒ∂ : IsPrimitiveRoot Œ∂ (2 ^ (k + 1)))
    [IsCyclotomicExtension {(2 : ‚Ñï+) ^ (k + 1)} K L]
    (hirr : Irreducible (cyclotomic (2 ^ (k + 1)) K)) :
    norm K (Œ∂ ^ 2 ^ k - 1) = (-2 : K) ^ 2 ^ k := by
  have := hŒ∂.pow_of_dvd (fun h => two_ne_zero (pow_eq_zero h)) (pow_dvd_pow 2 (le_succ k))
  -- ‚ä¢ ‚Üë(Algebra.norm K) (Œ∂ ^ 2 ^ k - 1) = (-2) ^ 2 ^ k
  rw [Nat.pow_div (le_succ k) zero_lt_two, Nat.succ_sub (le_refl k), Nat.sub_self, pow_one] at this
  -- ‚ä¢ ‚Üë(Algebra.norm K) (Œ∂ ^ 2 ^ k - 1) = (-2) ^ 2 ^ k
  have H : (-1 : L) - (1 : L) = algebraMap K L (-2) := by
    simp only [map_neg, map_ofNat]
    ring
-- Porting note: `simpa using hirr` was `simp [hirr]`.
  replace hirr : Irreducible (cyclotomic ((2 : ‚Ñï+) ^ (k + 1) : ‚Ñï+) K) := by simpa using hirr
  -- ‚ä¢ ‚Üë(Algebra.norm K) (Œ∂ ^ 2 ^ k - 1) = (-2) ^ 2 ^ k
-- Porting note: the proof is slightly different because of coercions.
  rw [this.eq_neg_one_of_two_right, H, Algebra.norm_algebraMap,
    IsCyclotomicExtension.finrank L hirr, pow_coe, show ((2 : ‚Ñï+) : ‚Ñï) = 2 from rfl,
      totient_prime_pow Nat.prime_two (zero_lt_succ k), succ_sub_succ_eq_sub, tsub_zero]
  simp
  -- üéâ no goals
#align is_primitive_root.pow_sub_one_norm_two IsPrimitiveRoot.pow_sub_one_norm_two

/-- If `Irreducible (cyclotomic (2 ^ k) K)` (in particular for `K = ‚Ñö`) and `k` is at least `2`,
then the norm of `Œ∂ - 1` is `2`. -/
theorem sub_one_norm_two {k : ‚Ñï} (hŒ∂ : IsPrimitiveRoot Œ∂ (2 ^ k)) (hk : 2 ‚â§ k)
    [H : IsCyclotomicExtension {(2 : ‚Ñï+) ^ k} K L] (hirr : Irreducible (cyclotomic (2 ^ k) K)) :
    norm K (Œ∂ - 1) = 2 := by
  have : 2 < (2 : ‚Ñï+) ^ k := by
    simp only [‚Üê coe_lt_coe, one_coe, pow_coe]
    nth_rw 1 [‚Üê pow_one 2]
    exact pow_lt_pow one_lt_two (lt_of_lt_of_le one_lt_two hk)
-- Porting note: `simpa using hirr` was `simp [hirr]`_
  replace hirr : Irreducible (cyclotomic ((2 : ‚Ñï+) ^ k : ‚Ñï+) K) := by simpa using hirr
  -- ‚ä¢ ‚Üë(Algebra.norm K) (Œ∂ - 1) = 2
-- Porting note: `simpa using hŒ∂` was `simp [hŒ∂]`_
  replace hŒ∂ : IsPrimitiveRoot Œ∂ ((2 : ‚Ñï+) ^ k) := by simpa using hŒ∂
  -- ‚ä¢ ‚Üë(Algebra.norm K) (Œ∂ - 1) = 2
  obtain ‚ü®k‚ÇÅ, hk‚ÇÅ‚ü© := exists_eq_succ_of_ne_zero (lt_of_lt_of_le zero_lt_two hk).ne.symm
  -- ‚ä¢ ‚Üë(Algebra.norm K) (Œ∂ - 1) = 2
-- Porting note: the proof is slightly different because of coercions.
  simpa [hk‚ÇÅ, show ((2 : ‚Ñï+) : ‚Ñï) = 2 from rfl] using sub_one_norm_eq_eval_cyclotomic hŒ∂ this hirr
  -- üéâ no goals
#align is_primitive_root.sub_one_norm_two IsPrimitiveRoot.sub_one_norm_two

/-- If `Irreducible (cyclotomic (p ^ (k + 1)) K)` (in particular for `K = ‚Ñö`) and `p` is a prime,
then the norm of `Œ∂ ^ (p ^ s) - 1` is `p ^ (p ^ s)` if `k ‚â† 0` and `s ‚â§ k`. -/
theorem pow_sub_one_norm_prime_pow_of_ne_zero {k s : ‚Ñï} (hŒ∂ : IsPrimitiveRoot Œ∂ ‚Üë(p ^ (k + 1)))
    [hpri : Fact (p : ‚Ñï).Prime] [hcycl : IsCyclotomicExtension {p ^ (k + 1)} K L]
    (hirr : Irreducible (cyclotomic (‚Üë(p ^ (k + 1)) : ‚Ñï) K)) (hs : s ‚â§ k) (hk : k ‚â† 0) :
    norm K (Œ∂ ^ (p : ‚Ñï) ^ s - 1) = (p : K) ^ (p : ‚Ñï) ^ s := by
  by_cases htwo : p ^ (k - s + 1) = 2
  -- ‚ä¢ ‚Üë(Algebra.norm K) (Œ∂ ^ ‚Üëp ^ s - 1) = ‚Üë‚Üëp ^ ‚Üëp ^ s
  ¬∑ have hp : p = 2 := by
      rw [‚Üê PNat.coe_inj, PNat.pow_coe, ‚Üê pow_one 2] at htwo
      replace htwo :=
        eq_of_prime_pow_eq (prime_iff.1 hpri.out) (prime_iff.1 Nat.prime_two) (succ_pos _) htwo
      rwa [show 2 = ((2 : ‚Ñï+) : ‚Ñï) by simp, PNat.coe_inj] at htwo
    replace hs : s = k
    -- ‚ä¢ s = k
    ¬∑ rw [hp, ‚Üê PNat.coe_inj, PNat.pow_coe] at htwo
      -- ‚ä¢ s = k
      nth_rw 2 [‚Üê pow_one 2] at htwo
      -- ‚ä¢ s = k
      replace htwo := Nat.pow_right_injective rfl.le htwo
      -- ‚ä¢ s = k
      rw [add_left_eq_self, Nat.sub_eq_zero_iff_le] at htwo
      -- ‚ä¢ s = k
      refine' le_antisymm hs htwo
      -- üéâ no goals
    simp only [hs, hp, one_coe, cast_one, pow_coe, show ((2 : ‚Ñï+) : ‚Ñï) = 2 from rfl]
      at hŒ∂ hirr hcycl ‚ä¢
    obtain ‚ü®k‚ÇÅ, hk‚ÇÅ‚ü© := Nat.exists_eq_succ_of_ne_zero hk
    -- ‚ä¢ ‚Üë(Algebra.norm K) (Œ∂ ^ 2 ^ k - 1) = ‚Üë2 ^ 2 ^ k
-- Porting note: the proof is slightly different because of coercions.
    rw [hŒ∂.pow_sub_one_norm_two hirr, hk‚ÇÅ, _root_.pow_succ, pow_mul, neg_eq_neg_one_mul, mul_pow,
      neg_one_sq, one_mul, ‚Üê pow_mul, ‚Üê _root_.pow_succ]
    simp
    -- üéâ no goals
  ¬∑ exact hŒ∂.pow_sub_one_norm_prime_pow_ne_two hirr hs htwo
    -- üéâ no goals
#align is_primitive_root.pow_sub_one_norm_prime_pow_of_ne_zero IsPrimitiveRoot.pow_sub_one_norm_prime_pow_of_ne_zero

end Field

end IsPrimitiveRoot

namespace IsCyclotomicExtension

open IsPrimitiveRoot

variable {K} (L) [Field K] [Field L] [Algebra K L]

/-- If `Irreducible (cyclotomic n K)` (in particular for `K = ‚Ñö`), the norm of `zeta n K L` is `1`
if `n` is odd. -/
theorem norm_zeta_eq_one [IsCyclotomicExtension {n} K L] (hn : n ‚â† 2)
    (hirr : Irreducible (cyclotomic n K)) : norm K (zeta n K L) = 1 :=
  (zeta_spec n K L).norm_eq_one hn hirr
#align is_cyclotomic_extension.norm_zeta_eq_one IsCyclotomicExtension.norm_zeta_eq_one

/-- If `IsPrimePow (n : ‚Ñï)`, `n ‚â† 2` and `Irreducible (cyclotomic n K)` (in particular for
`K = ‚Ñö`), then the norm of `zeta n K L - 1` is `(n : ‚Ñï).minFac`. -/
theorem isPrimePow_norm_zeta_sub_one (hn : IsPrimePow (n : ‚Ñï)) [IsCyclotomicExtension {n} K L]
    (hirr : Irreducible (cyclotomic (n : ‚Ñï) K)) (h : n ‚â† 2) :
    norm K (zeta n K L - 1) = (n : ‚Ñï).minFac :=
  (zeta_spec n K L).sub_one_norm_isPrimePow hn hirr h
#align is_cyclotomic_extension.is_prime_pow_norm_zeta_sub_one IsCyclotomicExtension.isPrimePow_norm_zeta_sub_one

/-- If `Irreducible (cyclotomic (p ^ (k + 1)) K)` (in particular for `K = ‚Ñö`) and `p` is a prime,
then the norm of `(zeta (p ^ (k + 1)) K L) ^ (p ^ s) - 1` is `p ^ (p ^ s)`
if `p ^ (k - s + 1) ‚â† 2`. -/
theorem prime_ne_two_pow_norm_zeta_pow_sub_one {k : ‚Ñï} [Fact (p : ‚Ñï).Prime]
    [IsCyclotomicExtension {p ^ (k + 1)} K L]
    (hirr : Irreducible (cyclotomic (‚Üë(p ^ (k + 1)) : ‚Ñï) K)) {s : ‚Ñï} (hs : s ‚â§ k)
    (htwo : p ^ (k - s + 1) ‚â† 2) :
    norm K (zeta (p ^ (k + 1)) K L ^ (p : ‚Ñï) ^ s - 1) = (p : K) ^ (p : ‚Ñï) ^ s :=
  (zeta_spec _ K L).pow_sub_one_norm_prime_pow_ne_two hirr hs htwo
#align is_cyclotomic_extension.prime_ne_two_pow_norm_zeta_pow_sub_one IsCyclotomicExtension.prime_ne_two_pow_norm_zeta_pow_sub_one

/-- If `Irreducible (cyclotomic (p ^ (k + 1)) K)` (in particular for `K = ‚Ñö`) and `p` is an odd
prime, then the norm of `zeta (p ^ (k + 1)) K L - 1` is `p`. -/
theorem prime_ne_two_pow_norm_zeta_sub_one {k : ‚Ñï} [Fact (p : ‚Ñï).Prime]
    [IsCyclotomicExtension {p ^ (k + 1)} K L]
    (hirr : Irreducible (cyclotomic (‚Üë(p ^ (k + 1)) : ‚Ñï) K)) (h : p ‚â† 2) :
    norm K (zeta (p ^ (k + 1)) K L - 1) = p :=
  (zeta_spec _ K L).sub_one_norm_prime_ne_two hirr h
#align is_cyclotomic_extension.prime_ne_two_pow_norm_zeta_sub_one IsCyclotomicExtension.prime_ne_two_pow_norm_zeta_sub_one

/-- If `Irreducible (cyclotomic p K)` (in particular for `K = ‚Ñö`) and `p` is an odd prime,
then the norm of `zeta p K L - 1` is `p`. -/
theorem prime_ne_two_norm_zeta_sub_one [Fact (p : ‚Ñï).Prime]
    [IsCyclotomicExtension {p} K L] (hirr : Irreducible (cyclotomic p K)) (h : p ‚â† 2) :
    norm K (zeta p K L - 1) = p :=
  (zeta_spec _ K L).sub_one_norm_prime hirr h
#align is_cyclotomic_extension.prime_ne_two_norm_zeta_sub_one IsCyclotomicExtension.prime_ne_two_norm_zeta_sub_one

/-- If `Irreducible (cyclotomic (2 ^ k) K)` (in particular for `K = ‚Ñö`) and `k` is at least `2`,
then the norm of `zeta (2 ^ k) K L - 1` is `2`. -/
theorem two_pow_norm_zeta_sub_one {k : ‚Ñï} (hk : 2 ‚â§ k) [IsCyclotomicExtension {(2 : ‚Ñï+) ^ k} K L]
    (hirr : Irreducible (cyclotomic (2 ^ k) K)) : norm K (zeta ((2 : ‚Ñï+) ^ k) K L - 1) = 2 :=
  sub_one_norm_two (zeta_spec ((2 : ‚Ñï+) ^ k) K L) hk hirr
#align is_cyclotomic_extension.two_pow_norm_zeta_sub_one IsCyclotomicExtension.two_pow_norm_zeta_sub_one

end IsCyclotomicExtension

end Norm
