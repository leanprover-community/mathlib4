/-
Copyright (c) 2022 Eric Rodriguez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Rodriguez
-/
import Mathlib.NumberTheory.Cyclotomic.PrimitiveRoots
import Mathlib.FieldTheory.PolynomialGaloisGroup

#align_import number_theory.cyclotomic.gal from "leanprover-community/mathlib"@"e3f4be1fcb5376c4948d7f095bec45350bfb9d1a"

/-!
# Galois group of cyclotomic extensions

In this file, we show the relationship between the Galois group of `K(Œ∂‚Çô)` and `(ZMod n)À£`;
it is always a subgroup, and if the `n`th cyclotomic polynomial is irreducible, they are isomorphic.

## Main results

* `IsPrimitiveRoot.autToPow_injective`: `IsPrimitiveRoot.autToPow` is injective
  in the case that it's considered over a cyclotomic field extension.
* `IsCyclotomicExtension.autEquivPow`: If the `n`th cyclotomic polynomial is irreducible in `K`,
  then `IsPrimitiveRoot.autToPow` is a `MulEquiv` (for example, in `‚Ñö` and certain `ùîΩ‚Çö`).
* `galXPowEquivUnitsZMod`, `galCyclotomicEquivUnitsZMod`: Repackage
  `IsCyclotomicExtension.autEquivPow` in terms of `Polynomial.Gal`.
* `IsCyclotomicExtension.Aut.commGroup`: Cyclotomic extensions are abelian.

## References

* https://kconrad.math.uconn.edu/blurbs/galoistheory/cyclotomic.pdf

## TODO

* We currently can get away with the fact that the power of a primitive root is a primitive root,
  but the correct long-term solution for computing other explicit Galois groups is creating
  `PowerBasis.map_conjugate`; but figuring out the exact correct assumptions + proof for this is
  mathematically nontrivial. (Current thoughts: the correct condition is that the annihilating
  ideal of both elements is equal. This may not hold in an ID, and definitely holds in an ICD.)

-/


variable {n : ‚Ñï+} (K : Type*) [Field K] {L : Type*} {Œº : L}

open Polynomial IsCyclotomicExtension

open scoped Cyclotomic

namespace IsPrimitiveRoot

variable [CommRing L] [IsDomain L] (hŒº : IsPrimitiveRoot Œº n) [Algebra K L]
  [IsCyclotomicExtension {n} K L]

/-- `IsPrimitiveRoot.autToPow` is injective in the case that it's considered over a cyclotomic
field extension. -/
theorem autToPow_injective : Function.Injective <| hŒº.autToPow K := by
  intro f g hfg
  apply_fun Units.val at hfg
  simp only [IsPrimitiveRoot.coe_autToPow_apply] at hfg
  generalize_proofs hf' hg' at hfg
  have hf := hf'.choose_spec
  have hg := hg'.choose_spec
  generalize_proofs hŒ∂ at hf hg
  suffices f (hŒº.toRootsOfUnity : LÀ£) = g (hŒº.toRootsOfUnity : LÀ£) by
    apply AlgEquiv.coe_algHom_injective
    apply (hŒº.powerBasis K).algHom_ext
    exact this
  rw [ZMod.eq_iff_modEq_nat] at hfg
  refine (hf.trans ?_).trans hg.symm
  rw [‚Üê rootsOfUnity.coe_pow _ hf'.choose, ‚Üê rootsOfUnity.coe_pow _ hg'.choose]
  congr 2
  rw [pow_eq_pow_iff_modEq]
  convert hfg
  rw [hŒº.eq_orderOf]
  -- Porting note: was `{occs := occurrences.pos [2]}`
  conv_rhs => rw [‚Üê hŒº.val_toRootsOfUnity_coe]
  rw [orderOf_units, Subgroup.orderOf_coe]
#align is_primitive_root.aut_to_pow_injective IsPrimitiveRoot.autToPow_injective

end IsPrimitiveRoot

namespace IsCyclotomicExtension

variable [CommRing L] [IsDomain L] (hŒº : IsPrimitiveRoot Œº n) [Algebra K L]
  [IsCyclotomicExtension {n} K L]

/-- Cyclotomic extensions are abelian. -/
noncomputable def Aut.commGroup : CommGroup (L ‚âÉ‚Çê[K] L) :=
  ((zeta_spec n K L).autToPow_injective K).commGroup _ (map_one _) (map_mul _) (map_inv _)
    (map_div _) (map_pow _) (map_zpow _)
#align is_cyclotomic_extension.aut.comm_group IsCyclotomicExtension.Aut.commGroup

variable (h : Irreducible (cyclotomic n K)) {K} (L)

/-- The `MulEquiv` that takes an automorphism `f` to the element `k : (ZMod n)À£` such that
  `f Œº = Œº ^ k` for any root of unity `Œº`. A strengthening of `IsPrimitiveRoot.autToPow`. -/
@[simps]
noncomputable def autEquivPow : (L ‚âÉ‚Çê[K] L) ‚âÉ* (ZMod n)À£ :=
  let hŒ∂ := zeta_spec n K L
  let hŒº t := hŒ∂.pow_of_coprime _ (ZMod.val_coe_unit_coprime t)
  { (zeta_spec n K L).autToPow K with
    invFun := fun t =>
      (hŒ∂.powerBasis K).equivOfMinpoly ((hŒº t).powerBasis K)
        (by
          haveI := IsCyclotomicExtension.neZero' n K L
          simp only [IsPrimitiveRoot.powerBasis_gen]
          have hr :=
            IsPrimitiveRoot.minpoly_eq_cyclotomic_of_irreducible
              ((zeta_spec n K L).pow_of_coprime _ (ZMod.val_coe_unit_coprime t)) h
          exact ((zeta_spec n K L).minpoly_eq_cyclotomic_of_irreducible h).symm.trans hr)
    left_inv := fun f => by
      simp only [MonoidHom.toFun_eq_coe]
      apply AlgEquiv.coe_algHom_injective
      apply (hŒ∂.powerBasis K).algHom_ext
-- Porting note: the proof is slightly different because of coercions.
      simp only [AlgHom.coe_coe]
      rw [PowerBasis.equivOfMinpoly_gen]
      simp only [IsPrimitiveRoot.powerBasis_gen, IsPrimitiveRoot.autToPow_spec]
    right_inv := fun x => by
      simp only [MonoidHom.toFun_eq_coe]
      generalize_proofs _ h
      have key := hŒ∂.autToPow_spec K ((hŒ∂.powerBasis K).equivOfMinpoly ((hŒº x).powerBasis K) h)
      have := (hŒ∂.powerBasis K).equivOfMinpoly_gen ((hŒº x).powerBasis K) h
      rw [hŒ∂.powerBasis_gen K] at this
      rw [this, IsPrimitiveRoot.powerBasis_gen] at key
-- Porting note: was `rw ‚Üê hŒ∂.coe_to_roots_of_unity_coe at key {occs := occurrences.pos [1, 5]}`.
      conv at key =>
        congr; congr
        rw [‚Üê hŒ∂.val_toRootsOfUnity_coe]
        rfl; rfl
        rw [‚Üê hŒ∂.val_toRootsOfUnity_coe]
      simp only [‚Üê rootsOfUnity.coe_pow] at key
      replace key := rootsOfUnity.coe_injective key
      rw [pow_eq_pow_iff_modEq, ‚Üê Subgroup.orderOf_coe, ‚Üê orderOf_units, hŒ∂.val_toRootsOfUnity_coe,
        ‚Üê (zeta_spec n K L).eq_orderOf, ‚Üê ZMod.eq_iff_modEq_nat] at key
      simp only [ZMod.natCast_val, ZMod.cast_id', id] at key
      exact Units.ext key }
#align is_cyclotomic_extension.aut_equiv_pow IsCyclotomicExtension.autEquivPow

variable {L}

/-- Maps `Œº` to the `AlgEquiv` that sends `IsCyclotomicExtension.zeta` to `Œº`. -/
noncomputable def fromZetaAut : L ‚âÉ‚Çê[K] L :=
  let hŒ∂ := (zeta_spec n K L).eq_pow_of_pow_eq_one hŒº.pow_eq_one n.pos
  (autEquivPow L h).symm <|
    ZMod.unitOfCoprime hŒ∂.choose <|
      ((zeta_spec n K L).pow_iff_coprime n.pos hŒ∂.choose).mp <| hŒ∂.choose_spec.2.symm ‚ñ∏ hŒº
#align is_cyclotomic_extension.from_zeta_aut IsCyclotomicExtension.fromZetaAut

theorem fromZetaAut_spec : fromZetaAut hŒº h (zeta n K L) = Œº := by
  simp_rw [fromZetaAut, autEquivPow_symm_apply]
  generalize_proofs hŒ∂ h _ hŒº _
  nth_rewrite 4 [‚Üê hŒ∂.powerBasis_gen K]
  rw [PowerBasis.equivOfMinpoly_gen, hŒº.powerBasis_gen K]
  convert h.choose_spec.2
  exact ZMod.val_cast_of_lt h.choose_spec.1
#align is_cyclotomic_extension.from_zeta_aut_spec IsCyclotomicExtension.fromZetaAut_spec

end IsCyclotomicExtension

section Gal

variable [Field L] (hŒº : IsPrimitiveRoot Œº n) [Algebra K L] [IsCyclotomicExtension {n} K L]
  (h : Irreducible (cyclotomic n K)) {K}

/-- `IsCyclotomicExtension.autEquivPow` repackaged in terms of `Gal`.
Asserts that the Galois group of `cyclotomic n K` is equivalent to `(ZMod n)À£`
if `cyclotomic n K` is irreducible in the base field. -/
noncomputable def galCyclotomicEquivUnitsZMod : (cyclotomic n K).Gal ‚âÉ* (ZMod n)À£ :=
  (AlgEquiv.autCongr
          (IsSplittingField.algEquiv L _ : L ‚âÉ‚Çê[K] (cyclotomic n K).SplittingField)).symm.trans
    (IsCyclotomicExtension.autEquivPow L h)
#align gal_cyclotomic_equiv_units_zmod galCyclotomicEquivUnitsZMod

/-- `IsCyclotomicExtension.autEquivPow` repackaged in terms of `Gal`.
Asserts that the Galois group of `X ^ n - 1` is equivalent to `(ZMod n)À£`
if `cyclotomic n K` is irreducible in the base field. -/
noncomputable def galXPowEquivUnitsZMod : (X ^ (n : ‚Ñï) - 1 : K[X]).Gal ‚âÉ* (ZMod n)À£ :=
  (AlgEquiv.autCongr
      (IsSplittingField.algEquiv L _ : L ‚âÉ‚Çê[K] (X ^ (n : ‚Ñï) - 1 : K[X]).SplittingField)).symm.trans
    (IsCyclotomicExtension.autEquivPow L h)
set_option linter.uppercaseLean3 false
#align gal_X_pow_equiv_units_zmod galXPowEquivUnitsZMod

end Gal
