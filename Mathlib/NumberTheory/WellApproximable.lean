/-
Copyright (c) 2022 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.Dynamics.Ergodic.AddCircle
import Mathlib.MeasureTheory.Covering.LiminfLimsup

#align_import number_theory.well_approximable from "leanprover-community/mathlib"@"f0c8bf9245297a541f468be517f1bde6195105e9"

/-!
# Well-approximable numbers and Gallagher's ergodic theorem

Gallagher's ergodic theorem is a result in metric number theory. It thus belongs to that branch of
mathematics concerning arithmetic properties of real numbers which hold almost eveywhere with
respect to the Lebesgue measure.

Gallagher's theorem concerns the approximation of real numbers by rational numbers. The input is a
sequence of distances `Œ¥‚ÇÅ, Œ¥‚ÇÇ, ...`, and the theorem concerns the set of real numbers `x` for which
there is an infinity of solutions to:
$$
  |x - m/n| < Œ¥‚Çô,
$$
where the rational number `m/n` is in lowest terms. The result is that for any `Œ¥`, this set is
either almost all `x` or almost no `x`.

This result was proved by Gallagher in 1959
[P. Gallagher, *Approximation by reduced fractions*](Gallagher1961). It is formalised here as
`AddCircle.addWellApproximable_ae_empty_or_univ` except with `x` belonging to the circle `‚Ñù ‚ß∏ ‚Ñ§`
since this turns out to be more natural.

Given a particular `Œ¥`, the Duffin-Schaeffer conjecture (now a theorem) gives a criterion for
deciding which of the two cases in the conclusion of Gallagher's theorem actually occurs. It was
proved by Koukoulopoulos and Maynard in 2019
[D. Koukoulopoulos, J. Maynard, *On the Duffin-Schaeffer conjecture*](KoukoulopoulosMaynard2020).
We do *not* include a formalisation of the Koukoulopoulos-Maynard result here.

## Main definitions and results:

 * `approxOrderOf`: in a seminormed group `A`, given `n : ‚Ñï` and `Œ¥ : ‚Ñù`, `approxOrderOf A n Œ¥`
   is the set of elements within a distance `Œ¥` of a point of order `n`.
 * `wellApproximable`: in a seminormed group `A`, given a sequence of distances `Œ¥‚ÇÅ, Œ¥‚ÇÇ, ...`,
   `wellApproximable A Œ¥` is the limsup as `n ‚Üí ‚àû` of the sets `approxOrderOf A n Œ¥‚Çô`. Thus, it
   is the set of points that lie in infinitely many of the sets `approxOrderOf A n Œ¥‚Çô`.
 * `AddCircle.addWellApproximable_ae_empty_or_univ`: *Gallagher's ergodic theorem* says that for
   the (additive) circle `ùïä`, for any sequence of distances `Œ¥`, the set
   `addWellApproximable ùïä Œ¥` is almost empty or almost full.
 * `NormedAddCommGroup.exists_norm_nsmul_le`: a general version of Dirichlet's approximation theorem
 * `AddCircle.exists_norm_nsmul_le`: Dirichlet's approximation theorem

## TODO:

The hypothesis `hŒ¥` in `AddCircle.addWellApproximable_ae_empty_or_univ` can be dropped.
An elementary (non-measure-theoretic) argument shows that if `¬¨ hŒ¥` holds then
`addWellApproximable ùïä Œ¥ = univ` (provided `Œ¥` is non-negative).

Use `AddCircle.exists_norm_nsmul_le` to prove:
`addWellApproximable ùïä (fun n ‚Ü¶ 1 / n^2) = { Œæ | ¬¨ IsOfFinAddOrder Œæ }`
(which is equivalent to `Real.infinite_rat_abs_sub_lt_one_div_den_sq_iff_irrational`).
-/


open Set Filter Function Metric MeasureTheory

open scoped MeasureTheory Topology Pointwise

/-- In a seminormed group `A`, given `n : ‚Ñï` and `Œ¥ : ‚Ñù`, `approxOrderOf A n Œ¥` is the set of
elements within a distance `Œ¥` of a point of order `n`. -/
@[to_additive "In a seminormed additive group `A`, given `n : ‚Ñï` and `Œ¥ : ‚Ñù`,
`approxAddOrderOf A n Œ¥` is the set of elements within a distance `Œ¥` of a point of order `n`."]
def approxOrderOf (A : Type*) [SeminormedGroup A] (n : ‚Ñï) (Œ¥ : ‚Ñù) : Set A :=
  thickening Œ¥ {y | orderOf y = n}
#align approx_order_of approxOrderOf
#align approx_add_order_of approxAddOrderOf

@[to_additive mem_approx_add_orderOf_iff]
theorem mem_approxOrderOf_iff {A : Type*} [SeminormedGroup A] {n : ‚Ñï} {Œ¥ : ‚Ñù} {a : A} :
    a ‚àà approxOrderOf A n Œ¥ ‚Üî ‚àÉ b : A, orderOf b = n ‚àß a ‚àà ball b Œ¥ := by
  simp only [approxOrderOf, thickening_eq_biUnion_ball, mem_iUnion‚ÇÇ, mem_setOf_eq, exists_prop]
#align mem_approx_order_of_iff mem_approxOrderOf_iff
#align mem_approx_add_order_of_iff mem_approx_add_orderOf_iff

/-- In a seminormed group `A`, given a sequence of distances `Œ¥‚ÇÅ, Œ¥‚ÇÇ, ...`, `wellApproximable A Œ¥`
is the limsup as `n ‚Üí ‚àû` of the sets `approxOrderOf A n Œ¥‚Çô`. Thus, it is the set of points that
lie in infinitely many of the sets `approxOrderOf A n Œ¥‚Çô`. -/
@[to_additive addWellApproximable "In a seminormed additive group `A`, given a sequence of
distances `Œ¥‚ÇÅ, Œ¥‚ÇÇ, ...`, `addWellApproximable A Œ¥` is the limsup as `n ‚Üí ‚àû` of the sets
`approxAddOrderOf A n Œ¥‚Çô`. Thus, it is the set of points that lie in infinitely many of the sets
`approxAddOrderOf A n Œ¥‚Çô`."]
def wellApproximable (A : Type*) [SeminormedGroup A] (Œ¥ : ‚Ñï ‚Üí ‚Ñù) : Set A :=
  blimsup (fun n => approxOrderOf A n (Œ¥ n)) atTop fun n => 0 < n
#align well_approximable wellApproximable
#align add_well_approximable addWellApproximable

@[to_additive mem_add_wellApproximable_iff]
theorem mem_wellApproximable_iff {A : Type*} [SeminormedGroup A] {Œ¥ : ‚Ñï ‚Üí ‚Ñù} {a : A} :
    a ‚àà wellApproximable A Œ¥ ‚Üî
      a ‚àà blimsup (fun n => approxOrderOf A n (Œ¥ n)) atTop fun n => 0 < n :=
  Iff.rfl
#align mem_well_approximable_iff mem_wellApproximable_iff
#align mem_add_well_approximable_iff mem_add_wellApproximable_iff

namespace approxOrderOf

variable {A : Type*} [SeminormedCommGroup A] {a : A} {m n : ‚Ñï} (Œ¥ : ‚Ñù)

@[to_additive]
theorem image_pow_subset_of_coprime (hm : 0 < m) (hmn : n.Coprime m) :
    (fun (y : A) => y ^ m) '' approxOrderOf A n Œ¥ ‚äÜ approxOrderOf A n (m * Œ¥) := by
  rintro - ‚ü®a, ha, rfl‚ü©
  obtain ‚ü®b, hb, hab‚ü© := mem_approxOrderOf_iff.mp ha
  replace hb : b ^ m ‚àà {u : A | orderOf u = n} := by
    rw [‚Üê hb] at hmn ‚ä¢; exact hmn.orderOf_pow
  apply ball_subset_thickening hb ((m : ‚Ñù) ‚Ä¢ Œ¥)
  convert pow_mem_ball hm hab using 1
  simp only [nsmul_eq_mul, Algebra.id.smul_eq_mul]
#align approx_order_of.image_pow_subset_of_coprime approxOrderOf.image_pow_subset_of_coprime
#align approx_add_order_of.image_nsmul_subset_of_coprime approxAddOrderOf.image_nsmul_subset_of_coprime

@[to_additive]
theorem image_pow_subset (n : ‚Ñï) (hm : 0 < m) :
    (fun (y : A) => y ^ m) '' approxOrderOf A (n * m) Œ¥ ‚äÜ approxOrderOf A n (m * Œ¥) := by
  rintro - ‚ü®a, ha, rfl‚ü©
  obtain ‚ü®b, hb : orderOf b = n * m, hab : a ‚àà ball b Œ¥‚ü© := mem_approxOrderOf_iff.mp ha
  replace hb : b ^ m ‚àà {y : A | orderOf y = n}
  ¬∑ rw [mem_setOf_eq, orderOf_pow' b hm.ne', hb, Nat.gcd_mul_left_left, n.mul_div_cancel hm]
  apply ball_subset_thickening hb (m * Œ¥)
  convert pow_mem_ball hm hab using 1
  simp only [nsmul_eq_mul]
#align approx_order_of.image_pow_subset approxOrderOf.image_pow_subset
#align approx_add_order_of.image_nsmul_subset approxAddOrderOf.image_nsmul_subset

@[to_additive]
theorem smul_subset_of_coprime (han : (orderOf a).Coprime n) :
    a ‚Ä¢ approxOrderOf A n Œ¥ ‚äÜ approxOrderOf A (orderOf a * n) Œ¥ := by
  simp_rw [approxOrderOf, thickening_eq_biUnion_ball, ‚Üê image_smul, image_iUnion‚ÇÇ, image_smul,
    smul_ball'', smul_eq_mul, mem_setOf_eq]
  refine' iUnion‚ÇÇ_subset_iff.mpr fun b hb c hc => _
  simp only [mem_iUnion, exists_prop]
  refine' ‚ü®a * b, _, hc‚ü©
  rw [‚Üê hb] at han ‚ä¢
  exact (Commute.all a b).orderOf_mul_eq_mul_orderOf_of_coprime han
#align approx_order_of.smul_subset_of_coprime approxOrderOf.smul_subset_of_coprime
#align approx_add_order_of.vadd_subset_of_coprime approxAddOrderOf.vadd_subset_of_coprime

@[to_additive vadd_eq_of_mul_dvd]
theorem smul_eq_of_mul_dvd (hn : 0 < n) (han : orderOf a ^ 2 ‚à£ n) :
    a ‚Ä¢ approxOrderOf A n Œ¥ = approxOrderOf A n Œ¥ := by
  simp_rw [approxOrderOf, thickening_eq_biUnion_ball, ‚Üê image_smul, image_iUnion‚ÇÇ, image_smul,
    smul_ball'', smul_eq_mul, mem_setOf_eq]
  replace han : ‚àÄ {b : A}, orderOf b = n ‚Üí orderOf (a * b) = n
  ¬∑ intro b hb
    rw [‚Üê hb] at han hn
    rw [sq] at han
    rwa [(Commute.all a b).orderOf_mul_eq_right_of_forall_prime_mul_dvd (orderOf_pos_iff.mp hn)
      fun p _ hp' => dvd_trans (mul_dvd_mul_right hp' <| orderOf a) han]
  let f : {b : A | orderOf b = n} ‚Üí {b : A | orderOf b = n} := fun b => ‚ü®a * b, han b.property‚ü©
  have hf : Surjective f := by
    rintro ‚ü®b, hb‚ü©
    refine' ‚ü®‚ü®a‚Åª¬π * b, _‚ü©, _‚ü©
    ¬∑ rw [mem_setOf_eq, ‚Üê orderOf_inv, mul_inv_rev, inv_inv, mul_comm]
      apply han
      simpa
    ¬∑ simp only [Subtype.mk_eq_mk, Subtype.coe_mk, mul_inv_cancel_left]
  simpa only [mem_setOf_eq, Subtype.coe_mk, iUnion_coe_set] using
    hf.iUnion_comp fun b => ball (b : A) Œ¥
#align approx_order_of.smul_eq_of_mul_dvd approxOrderOf.smul_eq_of_mul_dvd
#align approx_add_order_of.vadd_eq_of_mul_dvd approxAddOrderOf.vadd_eq_of_mul_dvd

end approxOrderOf

namespace UnitAddCircle

theorem mem_approxAddOrderOf_iff {Œ¥ : ‚Ñù} {x : UnitAddCircle} {n : ‚Ñï} (hn : 0 < n) :
    x ‚àà approxAddOrderOf UnitAddCircle n Œ¥ ‚Üî ‚àÉ m < n, gcd m n = 1 ‚àß ‚Äñx - ‚Üë((m : ‚Ñù) / n)‚Äñ < Œ¥ := by
  haveI := Real.fact_zero_lt_one
  simp only [mem_approx_add_orderOf_iff, mem_setOf_eq, ball, exists_prop, dist_eq_norm,
    AddCircle.addOrderOf_eq_pos_iff hn, mul_one]
  constructor
  ¬∑ rintro ‚ü®y, ‚ü®m, hm‚ÇÅ, hm‚ÇÇ, rfl‚ü©, hx‚ü©; exact ‚ü®m, hm‚ÇÅ, hm‚ÇÇ, hx‚ü©
  ¬∑ rintro ‚ü®m, hm‚ÇÅ, hm‚ÇÇ, hx‚ü©; exact ‚ü®‚Üë((m : ‚Ñù) / n), ‚ü®m, hm‚ÇÅ, hm‚ÇÇ, rfl‚ü©, hx‚ü©
#align unit_add_circle.mem_approx_add_order_of_iff UnitAddCircle.mem_approxAddOrderOf_iff

theorem mem_addWellApproximable_iff (Œ¥ : ‚Ñï ‚Üí ‚Ñù) (x : UnitAddCircle) :
    x ‚àà addWellApproximable UnitAddCircle Œ¥ ‚Üî
      {n : ‚Ñï | ‚àÉ m < n, gcd m n = 1 ‚àß ‚Äñx - ‚Üë((m : ‚Ñù) / n)‚Äñ < Œ¥ n}.Infinite := by
  simp only [mem_add_wellApproximable_iff, ‚Üê Nat.cofinite_eq_atTop, cofinite.blimsup_set_eq,
    mem_setOf_eq]
  refine' iff_of_eq (congr_arg Set.Infinite <| ext fun n => ‚ü®fun hn => _, fun hn => _‚ü©)
  ¬∑ exact (mem_approxAddOrderOf_iff hn.1).mp hn.2
  ¬∑ have h : 0 < n := by obtain ‚ü®m, hm‚ÇÅ, _, _‚ü© := hn; exact pos_of_gt hm‚ÇÅ
    exact ‚ü®h, (mem_approxAddOrderOf_iff h).mpr hn‚ü©
#align unit_add_circle.mem_add_well_approximable_iff UnitAddCircle.mem_addWellApproximable_iff

end UnitAddCircle

namespace AddCircle

variable {T : ‚Ñù} [hT : Fact (0 < T)]

local notation a "‚à§" b => ¬¨a ‚à£ b

local notation a "‚à£‚à£" b => a ‚à£ b ‚àß (a * a)‚à§b

local notation "ùïä" => AddCircle T

/-- **Gallagher's ergodic theorem** on Diophantine approximation. -/
theorem addWellApproximable_ae_empty_or_univ (Œ¥ : ‚Ñï ‚Üí ‚Ñù) (hŒ¥ : Tendsto Œ¥ atTop (ùìù 0)) :
    (‚àÄ·µê x, ¬¨addWellApproximable ùïä Œ¥ x) ‚à® ‚àÄ·µê x, addWellApproximable ùïä Œ¥ x := by
  /- Sketch of proof:

    Let `E := addWellApproximable ùïä Œ¥`. For each prime `p : ‚Ñï`, we can partition `E` into three
    pieces `E = (A p) ‚à™ (B p) ‚à™ (C p)` where:
      `A p = blimsup (approxAddOrderOf ùïä n (Œ¥ n)) atTop (fun n => 0 < n ‚àß (p ‚à§ n))`
      `B p = blimsup (approxAddOrderOf ùïä n (Œ¥ n)) atTop (fun n => 0 < n ‚àß (p ‚à£‚à£ n))`
      `C p = blimsup (approxAddOrderOf ùïä n (Œ¥ n)) atTop (fun n => 0 < n ‚àß (p*p ‚à£ n))`.
    In other words, `A p` is the set of points `x` for which there exist infinitely-many `n` such
    that `x` is within a distance `Œ¥ n` of a point of order `n` and `p ‚à§ n`. Similarly for `B`, `C`.

    These sets have the following key properties:
      1. `A p` is almost invariant under the ergodic map `y ‚Ü¶ p ‚Ä¢ y`
      2. `B p` is almost invariant under the ergodic map `y ‚Ü¶ p ‚Ä¢ y + 1/p`
      3. `C p` is invariant under the map `y ‚Ü¶ y + 1/p`
    To prove 1 and 2 we need the key result `blimsup_thickening_mul_ae_eq` but 3 is elementary.

    It follows from `AddCircle.ergodic_nsmul_add` and `Ergodic.ae_empty_or_univ_of_image_ae_le` that
    if either `A p` or `B p` is not almost empty for any `p`, then it is almost full and thus so is
    `E`. We may therefore assume that `A p` and `B p` are almost empty for all `p`. We thus have
    `E` is almost equal to `C p` for every prime. Combining this with 3 we find that `E` is almost
    invariant under the map `y ‚Ü¶ y + 1/p` for every prime `p`. The required result then follows from
    `AddCircle.ae_empty_or_univ_of_forall_vadd_ae_eq_self`. -/
  letI : SemilatticeSup Nat.Primes := Nat.Subtype.semilatticeSup _
  set Œº : Measure ùïä := volume
  set u : Nat.Primes ‚Üí ùïä := fun p => ‚Üë((‚Üë(1 : ‚Ñï) : ‚Ñù) / ((p : ‚Ñï) : ‚Ñù) * T)
  have hu‚ÇÄ : ‚àÄ p : Nat.Primes, addOrderOf (u p) = (p : ‚Ñï) := by
    rintro ‚ü®p, hp‚ü©; exact addOrderOf_div_of_gcd_eq_one hp.pos (gcd_one_left p)
  have hu : Tendsto (addOrderOf ‚àò u) atTop atTop := by
    rw [(funext hu‚ÇÄ : addOrderOf ‚àò u = (‚Üë))]
    have h_mono : Monotone ((‚Üë) : Nat.Primes ‚Üí ‚Ñï) := fun p q hpq => hpq
    refine' h_mono.tendsto_atTop_atTop fun n => _
    obtain ‚ü®p, hp, hp'‚ü© := n.exists_infinite_primes
    exact ‚ü®‚ü®p, hp'‚ü©, hp‚ü©
  set E := addWellApproximable ùïä Œ¥
  set X : ‚Ñï ‚Üí Set ùïä := fun n => approxAddOrderOf ùïä n (Œ¥ n)
  set A : ‚Ñï ‚Üí Set ùïä := fun p => blimsup X atTop fun n => 0 < n ‚àß p‚à§n
  set B : ‚Ñï ‚Üí Set ùïä := fun p => blimsup X atTop fun n => 0 < n ‚àß p‚à£‚à£n
  set C : ‚Ñï ‚Üí Set ùïä := fun p => blimsup X atTop fun n => 0 < n ‚àß p ^ 2 ‚à£ n
  have hA‚ÇÄ : ‚àÄ p, MeasurableSet (A p) := fun p =>
    MeasurableSet.measurableSet_blimsup fun n _ => isOpen_thickening.measurableSet
  have hB‚ÇÄ : ‚àÄ p, MeasurableSet (B p) := fun p =>
    MeasurableSet.measurableSet_blimsup fun n _ => isOpen_thickening.measurableSet
  have hE‚ÇÄ : NullMeasurableSet E Œº := by
    refine' (MeasurableSet.measurableSet_blimsup fun n hn =>
      IsOpen.measurableSet _).nullMeasurableSet
    exact isOpen_thickening
  have hE‚ÇÅ : ‚àÄ p, E = A p ‚à™ B p ‚à™ C p := by
    intro p
    simp only [addWellApproximable, ‚Üê blimsup_or_eq_sup, ‚Üê and_or_left, ‚Üê sup_eq_union, sq]
    congr
    ext n
    tauto
  have hE‚ÇÇ : ‚àÄ p : Nat.Primes, A p =·µê[Œº] (‚àÖ : Set ùïä) ‚àß B p =·µê[Œº] (‚àÖ : Set ùïä) ‚Üí E =·µê[Œº] C p := by
    rintro p ‚ü®hA, hB‚ü©
    rw [hE‚ÇÅ p]
    exact union_ae_eq_right_of_ae_eq_empty ((union_ae_eq_right_of_ae_eq_empty hA).trans hB)
  have hA : ‚àÄ p : Nat.Primes, A p =·µê[Œº] (‚àÖ : Set ùïä) ‚à® A p =·µê[Œº] univ := by
    rintro ‚ü®p, hp‚ü©
    let f : ùïä ‚Üí ùïä := fun y => (p : ‚Ñï) ‚Ä¢ y
    suffices
      f '' A p ‚äÜ blimsup (fun n => approxAddOrderOf ùïä n (p * Œ¥ n)) atTop fun n => 0 < n ‚àß p‚à§n by
      apply (ergodic_nsmul hp.one_lt).ae_empty_or_univ_of_image_ae_le (hA‚ÇÄ p)
      apply (HasSubset.Subset.eventuallyLE this).congr EventuallyEq.rfl
      exact blimsup_thickening_mul_ae_eq Œº (fun n => 0 < n ‚àß p‚à§n) (fun n => {y | addOrderOf y = n})
        (Nat.cast_pos.mpr hp.pos) _ hŒ¥
    refine' (SupHom.apply_blimsup_le (sSupHom.setImage f)).trans (mono_blimsup fun n hn => _)
    replace hn := Nat.coprime_comm.mp (hp.coprime_iff_not_dvd.2 hn.2)
    exact approxAddOrderOf.image_nsmul_subset_of_coprime (Œ¥ n) hp.pos hn
  have hB : ‚àÄ p : Nat.Primes, B p =·µê[Œº] (‚àÖ : Set ùïä) ‚à® B p =·µê[Œº] univ := by
    rintro ‚ü®p, hp‚ü©
    let x := u ‚ü®p, hp‚ü©
    let f : ùïä ‚Üí ùïä := fun y => p ‚Ä¢ y + x
    suffices
      f '' B p ‚äÜ blimsup (fun n => approxAddOrderOf ùïä n (p * Œ¥ n)) atTop fun n => 0 < n ‚àß p‚à£‚à£n by
      apply (ergodic_nsmul_add x hp.one_lt).ae_empty_or_univ_of_image_ae_le (hB‚ÇÄ p)
      apply (HasSubset.Subset.eventuallyLE this).congr EventuallyEq.rfl
      exact blimsup_thickening_mul_ae_eq Œº (fun n => 0 < n ‚àß p‚à£‚à£n) (fun n => {y | addOrderOf y = n})
        (Nat.cast_pos.mpr hp.pos) _ hŒ¥
    refine' (SupHom.apply_blimsup_le (sSupHom.setImage f)).trans (mono_blimsup _)
    rintro n ‚ü®hn, h_div, h_ndiv‚ü©
    have h_cop : (addOrderOf x).Coprime (n / p) := by
      obtain ‚ü®q, rfl‚ü© := h_div
      rw [hu‚ÇÄ, Subtype.coe_mk, hp.coprime_iff_not_dvd, q.mul_div_cancel_left hp.pos]
      exact fun contra => h_ndiv (mul_dvd_mul_left p contra)
    replace h_div : n / p * p = n := Nat.div_mul_cancel h_div
    have hf : f = (fun y => x + y) ‚àò fun y => p ‚Ä¢ y := by ext; simp [add_comm x]; ac_rfl
    simp only at hf
    simp_rw [Function.comp_apply, le_eq_subset]
    rw [sSupHom.setImage_toFun, hf, image_comp]
    have := @monotone_image ùïä ùïä fun y => x + y
    specialize this (approxAddOrderOf.image_nsmul_subset (Œ¥ n) (n / p) hp.pos)
    simp only [h_div] at this ‚ä¢
    refine' this.trans _
    convert approxAddOrderOf.vadd_subset_of_coprime (p * Œ¥ n) h_cop
    rw [hu‚ÇÄ, Subtype.coe_mk, mul_comm p, h_div]
  change (‚àÄ·µê x, x ‚àâ E) ‚à® E ‚àà volume.ae
  rw [‚Üê eventuallyEq_empty, ‚Üê eventuallyEq_univ]
  have hC : ‚àÄ p : Nat.Primes, u p +·µ• C p = C p := by
    intro p
    let e := (AddAction.toPerm (u p) : Equiv.Perm ùïä).toOrderIsoSet
    change e (C p) = C p
    rw [OrderIso.apply_blimsup e, ‚Üê hu‚ÇÄ p]
    exact blimsup_congr (eventually_of_forall fun n hn =>
      approxAddOrderOf.vadd_eq_of_mul_dvd (Œ¥ n) hn.1 hn.2)
  by_cases h : ‚àÄ p : Nat.Primes, A p =·µê[Œº] (‚àÖ : Set ùïä) ‚àß B p =·µê[Œº] (‚àÖ : Set ùïä)
  ¬∑ replace h : ‚àÄ p : Nat.Primes, (u p +·µ• E : Set _) =·µê[Œº] E
    ¬∑ intro p
      replace hE‚ÇÇ : E =·µê[Œº] C p := hE‚ÇÇ p (h p)
      have h_qmp : Measure.QuasiMeasurePreserving (-u p +·µ• ¬∑) Œº Œº :=
        (measurePreserving_vadd _ Œº).quasiMeasurePreserving
      refine' (h_qmp.vadd_ae_eq_of_ae_eq (u p) hE‚ÇÇ).trans (ae_eq_trans _ hE‚ÇÇ.symm)
      rw [hC]
    exact ae_empty_or_univ_of_forall_vadd_ae_eq_self hE‚ÇÄ h hu
  ¬∑ right
    simp only [not_forall, not_and_or] at h
    obtain ‚ü®p, hp‚ü© := h
    rw [hE‚ÇÅ p]
    cases hp
    ¬∑ cases' hA p with _ h; ¬∑ contradiction
      -- Porting note: was `simp only [h, union_ae_eq_univ_of_ae_eq_univ_left]`
      have := union_ae_eq_univ_of_ae_eq_univ_left (t := B ‚Üëp) h
      exact union_ae_eq_univ_of_ae_eq_univ_left (t := C ‚Üëp) this
    ¬∑ cases' hB p with _ h; ¬∑ contradiction
      -- Porting note: was
      -- `simp only [h, union_ae_eq_univ_of_ae_eq_univ_left, union_ae_eq_univ_of_ae_eq_univ_right]`
      have := union_ae_eq_univ_of_ae_eq_univ_right (s := A ‚Üëp) h
      exact union_ae_eq_univ_of_ae_eq_univ_left (t := C ‚Üëp) this
#align add_circle.add_well_approximable_ae_empty_or_univ AddCircle.addWellApproximable_ae_empty_or_univ

/-- A general version of **Dirichlet's approximation theorem**.

See also `AddCircle.exists_norm_nsmul_le`. -/
lemma _root_.NormedAddCommGroup.exists_norm_nsmul_le {A : Type*}
    [NormedAddCommGroup A] [CompactSpace A] [ConnectedSpace A]
    [MeasurableSpace A] [BorelSpace A] {Œº : Measure A} [Œº.IsAddHaarMeasure]
    (Œæ : A) {n : ‚Ñï} (hn : 0 < n) (Œ¥ : ‚Ñù) (hŒ¥ : Œº univ ‚â§ (n + 1) ‚Ä¢ Œº (closedBall (0 : A) (Œ¥/2))) :
    ‚àÉ j ‚àà Icc 1 n, ‚Äñj ‚Ä¢ Œæ‚Äñ ‚â§ Œ¥ := by
  have : IsFiniteMeasure Œº := CompactSpace.isFiniteMeasure
  let B : Icc 0 n ‚Üí Set A := fun j ‚Ü¶ closedBall ((j : ‚Ñï) ‚Ä¢ Œæ) (Œ¥/2)
  have hB : ‚àÄ j, IsClosed (B j) := fun j ‚Ü¶ isClosed_ball
  suffices : ¬¨ Pairwise (Disjoint on B)
  ¬∑ obtain ‚ü®i, j, hij, x, hx‚ü© := exists_lt_mem_inter_of_not_pairwise_disjoint this
    refine' ‚ü®j - i, ‚ü®le_tsub_of_add_le_left hij, _‚ü©, _‚ü©
    ¬∑ simpa only [tsub_le_iff_right] using j.property.2.trans le_self_add
    ¬∑ rw [sub_nsmul _ (Subtype.coe_le_coe.mpr hij.le), ‚Üê sub_eq_add_neg, ‚Üê dist_eq_norm]
      refine' (dist_triangle (‚Üëj ‚Ä¢ Œæ) x (‚Üëi ‚Ä¢ Œæ)).trans _
      linarith [mem_closedBall.mp hx.1, mem_closedBall'.mp hx.2]
  by_contra h
  apply hn.ne'
  have h' : ‚ãÉ j, B j = univ := by
    rw [‚Üê (isClosed_iUnion_of_finite hB).measure_eq_univ_iff_eq (Œº := Œº)]
    refine' le_antisymm (Œº.mono (subset_univ _)) _
    simp_rw [measure_iUnion h (fun _ ‚Ü¶ measurableSet_closedBall), tsum_fintype,
      Œº.addHaar_closedBall_center, Finset.sum_const, Finset.card_univ, Nat.card_fintypeIcc,
      tsub_zero]
    exact hŒ¥
  replace hŒ¥ : 0 ‚â§ Œ¥/2 := by
    by_contra contra
    suffices : Œº (closedBall 0 (Œ¥/2)) = 0
    ¬∑ apply isOpen_univ.measure_ne_zero Œº univ_nonempty $ le_zero_iff.mp $ le_trans hŒ¥ _
      simp [this]
    rw [not_le, ‚Üê closedBall_eq_empty (x := (0 : A))] at contra
    simp [contra]
  have h'' : ‚àÄ j, (B j).Nonempty := by intro j; rwa [nonempty_closedBall]
  simpa using subsingleton_of_disjoint_isClosed_iUnion_eq_univ h'' h hB h'

/-- **Dirichlet's approximation theorem**

See also `Real.exists_rat_abs_sub_le_and_den_le`. -/
lemma exists_norm_nsmul_le (Œæ : ùïä) {n : ‚Ñï} (hn : 0 < n) :
    ‚àÉ j ‚àà Icc 1 n, ‚Äñj ‚Ä¢ Œæ‚Äñ ‚â§ T / ‚Üë(n + 1) := by
  apply NormedAddCommGroup.exists_norm_nsmul_le (Œº := volume) Œæ hn
  rw [AddCircle.measure_univ, volume_closedBall, ‚Üê ENNReal.ofReal_nsmul,
    mul_div_cancel' _ two_ne_zero, min_eq_right (div_le_self hT.out.le $ by simp), nsmul_eq_mul,
    mul_div_cancel' _ (Nat.cast_ne_zero.mpr n.succ_ne_zero)]

end AddCircle
