/-
Copyright (c) 2023 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.NumberTheory.LSeries.HurwitzZeta
import Mathlib.Analysis.PSeriesComplex

/-!
# Definition of the Riemann zeta function

## Main definitions:

* `riemannZeta`: the Riemann zeta function `Î¶ : â„‚ â†’ â„‚`.
* `completedRiemannZeta`: the completed zeta function `Î› : â„‚ â†’ â„‚`, which satisfies
  `Î›(s) = Ï€ ^ (-s / 2) Î“(s / 2) Î¶(s)` (away from the poles of `Î“(s / 2)`).
* `completedRiemannZetaâ‚€`: the entire function `Î›â‚€` satisfying
  `Î›â‚€(s) = Î›(s) + 1 / (s - 1) - 1 / s` wherever the RHS is defined.

Note that mathematically `Î¶(s)` is undefined at `s = 1`, while `Î›(s)` is undefined at both `s = 0`
and `s = 1`. Our construction assigns some values at these points; exact formulae involving the
Euler-Mascheroni constant will follow in a subsequent PR.

## Main results:

* `differentiable_completedZetaâ‚€` : the function `Î›â‚€(s)` is entire.
* `differentiableAt_completedZeta` : the function `Î›(s)` is differentiable away from `s = 0` and
  `s = 1`.
* `differentiableAt_riemannZeta` : the function `Î¶(s)` is differentiable away from `s = 1`.
* `zeta_eq_tsum_one_div_nat_add_one_cpow` : for `1 < re s`, we have
  `Î¶(s) = âˆ‘' (n : â„•), 1 / (n + 1) ^ s`.
* `completedRiemannZetaâ‚€_one_sub`, `completedRiemannZeta_one_sub`, and `riemannZeta_one_sub` :
  functional equation relating values at `s` and `1 - s`

For special-value formulae expressing `Î¶ (2 * k)` and `Î¶ (1 - 2 * k)` in terms of Bernoulli numbers
see `Mathlib/NumberTheory/LSeries/HurwitzZetaValues.lean`. For computation of the constant term as
`s â†’ 1`, see `Mathlib/NumberTheory/Harmonic/ZetaAsymp.lean`.

## Outline of proofs:

These results are mostly special cases of more general results for even Hurwitz zeta functions
proved in `Mathlib/NumberTheory/LSeries/HurwitzZetaEven.lean`.
-/


open CharZero Set Filter HurwitzZeta

open Complex hiding exp continuous_exp

open scoped Topology Real

noncomputable section

/-!
## Definition of the completed Riemann zeta
-/

/-- The completed Riemann zeta function with its poles removed, `Î›(s) + 1 / s - 1 / (s - 1)`. -/
def completedRiemannZetaâ‚€ (s : â„‚) : â„‚ := completedHurwitzZetaEvenâ‚€ 0 s

/-- The completed Riemann zeta function, `Î›(s)`, which satisfies
`Î›(s) = Ï€ ^ (-s / 2) Î“(s / 2) Î¶(s)` (up to a minor correction at `s = 0`). -/
def completedRiemannZeta (s : â„‚) : â„‚ := completedHurwitzZetaEven 0 s

lemma HurwitzZeta.completedHurwitzZetaEven_zero (s : â„‚) :
    completedHurwitzZetaEven 0 s = completedRiemannZeta s := rfl

lemma HurwitzZeta.completedHurwitzZetaEvenâ‚€_zero (s : â„‚) :
    completedHurwitzZetaEvenâ‚€ 0 s = completedRiemannZetaâ‚€ s := rfl

lemma HurwitzZeta.completedCosZeta_zero (s : â„‚) :
    completedCosZeta 0 s = completedRiemannZeta s := by
  rw [completedRiemannZeta, completedHurwitzZetaEven, completedCosZeta, hurwitzEvenFEPair_zero_symm]

lemma HurwitzZeta.completedCosZetaâ‚€_zero (s : â„‚) :
    completedCosZetaâ‚€ 0 s = completedRiemannZetaâ‚€ s := by
  rw [completedRiemannZetaâ‚€, completedHurwitzZetaEvenâ‚€, completedCosZetaâ‚€,
    hurwitzEvenFEPair_zero_symm]

lemma completedRiemannZeta_eq (s : â„‚) :
    completedRiemannZeta s = completedRiemannZetaâ‚€ s - 1 / s - 1 / (1 - s) := by
  simp_rw [completedRiemannZeta, completedRiemannZetaâ‚€, completedHurwitzZetaEven_eq, if_true]

/-- The modified completed Riemann zeta function `Î›(s) + 1 / s + 1 / (1 - s)` is entire. -/
theorem differentiable_completedZetaâ‚€ : Differentiable â„‚ completedRiemannZetaâ‚€ :=
  differentiable_completedHurwitzZetaEvenâ‚€ 0

/-- The completed Riemann zeta function `Î›(s)` is differentiable away from `s = 0` and `s = 1`. -/
theorem differentiableAt_completedZeta {s : â„‚} (hs : s â‰  0) (hs' : s â‰  1) :
    DifferentiableAt â„‚ completedRiemannZeta s :=
  differentiableAt_completedHurwitzZetaEven 0 (Or.inl hs) hs'

/-- Riemann zeta functional equation, formulated for `Î›â‚€`: for any complex `s` we have
`Î›â‚€(1 - s) = Î›â‚€ s`. -/
theorem completedRiemannZetaâ‚€_one_sub (s : â„‚) :
    completedRiemannZetaâ‚€ (1 - s) = completedRiemannZetaâ‚€ s := by
  rw [â† completedHurwitzZetaEvenâ‚€_zero, â† completedCosZetaâ‚€_zero, completedHurwitzZetaEvenâ‚€_one_sub]

/-- Riemann zeta functional equation, formulated for `Î›`: for any complex `s` we have
`Î› (1 - s) = Î› s`. -/
theorem completedRiemannZeta_one_sub (s : â„‚) :
    completedRiemannZeta (1 - s) = completedRiemannZeta s := by
  rw [â† completedHurwitzZetaEven_zero, â† completedCosZeta_zero, completedHurwitzZetaEven_one_sub]

/-- The residue of `Î›(s)` at `s = 1` is equal to `1`. -/
lemma completedRiemannZeta_residue_one :
    Tendsto (fun s â†¦ (s - 1) * completedRiemannZeta s) (ğ“[â‰ ] 1) (ğ“ 1) :=
  completedHurwitzZetaEven_residue_one 0

/-!
## The un-completed Riemann zeta function
-/

/-- The Riemann zeta function `Î¶(s)`. -/
def riemannZeta := hurwitzZetaEven 0

lemma HurwitzZeta.hurwitzZetaEven_zero : hurwitzZetaEven 0 = riemannZeta := rfl

lemma HurwitzZeta.cosZeta_zero : cosZeta 0 = riemannZeta := by
  simp_rw [cosZeta, riemannZeta, hurwitzZetaEven, if_true, completedHurwitzZetaEven_zero,
    completedCosZeta_zero]

lemma HurwitzZeta.hurwitzZeta_zero : hurwitzZeta 0 = riemannZeta := by
  ext1 s
  simpa [hurwitzZeta, hurwitzZetaEven_zero] using hurwitzZetaOdd_neg 0 s

lemma HurwitzZeta.expZeta_zero : expZeta 0 = riemannZeta := by
  ext1 s
  rw [expZeta, cosZeta_zero, add_eq_left, mul_eq_zero, eq_false_intro I_ne_zero, false_or,
    â† eq_neg_self_iff, â† sinZeta_neg, neg_zero]

/-- The Riemann zeta function is differentiable away from `s = 1`. -/
theorem differentiableAt_riemannZeta {s : â„‚} (hs' : s â‰  1) : DifferentiableAt â„‚ riemannZeta s :=
  differentiableAt_hurwitzZetaEven _ hs'

/-- We have `Î¶(0) = -1 / 2`. -/
theorem riemannZeta_zero : riemannZeta 0 = -1 / 2 := by
  simp_rw [riemannZeta, hurwitzZetaEven, Function.update_self, if_true]

lemma riemannZeta_def_of_ne_zero {s : â„‚} (hs : s â‰  0) :
    riemannZeta s = completedRiemannZeta s / Gammaâ„ s := by
  rw [riemannZeta, hurwitzZetaEven, Function.update_of_ne hs, completedHurwitzZetaEven_zero]

/-- The trivial zeroes of the zeta function. -/
theorem riemannZeta_neg_two_mul_nat_add_one (n : â„•) : riemannZeta (-2 * (n + 1)) = 0 :=
  hurwitzZetaEven_neg_two_mul_nat_add_one 0 n

/-- Riemann zeta functional equation, formulated for `Î¶`: if `1 - s âˆ‰ â„•`, then we have
`Î¶ (1 - s) = 2 ^ (1 - s) * Ï€ ^ (-s) * Î“ s * sin (Ï€ * (1 - s) / 2) * Î¶ s`. -/
theorem riemannZeta_one_sub {s : â„‚} (hs : âˆ€ n : â„•, s â‰  -n) (hs' : s â‰  1) :
    riemannZeta (1 - s) = 2 * (2 * Ï€) ^ (-s) * Gamma s * cos (Ï€ * s / 2) * riemannZeta s := by
  rw [riemannZeta, hurwitzZetaEven_one_sub 0 hs (Or.inr hs'), cosZeta_zero, hurwitzZetaEven_zero]

/-- A formal statement of the **Riemann hypothesis** â€“ constructing a term of this type is worth a
million dollars. -/
def RiemannHypothesis : Prop :=
  âˆ€ (s : â„‚) (_ : riemannZeta s = 0) (_ : Â¬âˆƒ n : â„•, s = -2 * (n + 1)) (_ : s â‰  1), s.re = 1 / 2

/-!
## Relating the Mellin transform to the Dirichlet series
-/

theorem completedZeta_eq_tsum_of_one_lt_re {s : â„‚} (hs : 1 < re s) :
    completedRiemannZeta s =
      (Ï€ : â„‚) ^ (-s / 2) * Gamma (s / 2) * âˆ‘' n : â„•, 1 / (n : â„‚) ^ s := by
  have := (hasSum_nat_completedCosZeta 0 hs).tsum_eq.symm
  simp only [QuotientAddGroup.mk_zero, completedCosZeta_zero] at this
  simp only [this, Gammaâ„_def, mul_zero, zero_mul, Real.cos_zero, ofReal_one, mul_one, mul_one_div,
    â† tsum_mul_left]
  congr 1 with n
  split_ifs with h
  Â· simp only [h, Nat.cast_zero, zero_cpow (Complex.ne_zero_of_one_lt_re hs), div_zero]
  Â· rfl

/-- The Riemann zeta function agrees with the naive Dirichlet-series definition when the latter
converges. (Note that this is false without the assumption: when `re s â‰¤ 1` the sum is divergent,
and we use a different definition to obtain the analytic continuation to all `s`.) -/
theorem zeta_eq_tsum_one_div_nat_cpow {s : â„‚} (hs : 1 < re s) :
    riemannZeta s = âˆ‘' n : â„•, 1 / (n : â„‚) ^ s := by
  simpa only [QuotientAddGroup.mk_zero, cosZeta_zero, mul_zero, zero_mul, Real.cos_zero,
    ofReal_one] using (hasSum_nat_cosZeta 0 hs).tsum_eq.symm

/-- Alternate formulation of `zeta_eq_tsum_one_div_nat_cpow` with a `+ 1` (to avoid relying
on mathlib's conventions for `0 ^ s`). -/
theorem zeta_eq_tsum_one_div_nat_add_one_cpow {s : â„‚} (hs : 1 < re s) :
    riemannZeta s = âˆ‘' n : â„•, 1 / (n + 1 : â„‚) ^ s := by
  have := zeta_eq_tsum_one_div_nat_cpow hs
  rw [Summable.tsum_eq_zero_add] at this
  Â· simpa [zero_cpow (Complex.ne_zero_of_one_lt_re hs)]
  Â· rwa [Complex.summable_one_div_nat_cpow]

/-- Special case of `zeta_eq_tsum_one_div_nat_cpow` when the argument is in `â„•`, so the power
function can be expressed using naÃ¯ve `pow` rather than `cpow`. -/
theorem zeta_nat_eq_tsum_of_gt_one {k : â„•} (hk : 1 < k) :
    riemannZeta k = âˆ‘' n : â„•, 1 / (n : â„‚) ^ k := by
  simp only [zeta_eq_tsum_one_div_nat_cpow
      (by rwa [â† ofReal_natCast, ofReal_re, â† Nat.cast_one, Nat.cast_lt] : 1 < re k),
    cpow_natCast]

/-- The residue of `Î¶(s)` at `s = 1` is equal to 1. -/
lemma riemannZeta_residue_one : Tendsto (fun s â†¦ (s - 1) * riemannZeta s) (ğ“[â‰ ] 1) (ğ“ 1) := by
  exact hurwitzZetaEven_residue_one 0

/-- The residue of `Î¶(s)` at `s = 1` is equal to 1, expressed using `tsum`. -/
theorem tendsto_sub_mul_tsum_nat_cpow :
    Tendsto (fun s : â„‚ â†¦ (s - 1) * âˆ‘' (n : â„•), 1 / (n : â„‚) ^ s) (ğ“[{s | 1 < re s}] 1) (ğ“ 1) := by
  refine (tendsto_nhdsWithin_mono_left ?_ riemannZeta_residue_one).congr' ?_
  Â· simp only [subset_compl_singleton_iff, mem_setOf_eq, one_re, not_lt, le_refl]
  Â· filter_upwards [eventually_mem_nhdsWithin] with s hs using
      congr_arg _ <| zeta_eq_tsum_one_div_nat_cpow hs

/-- The residue of `Î¶(s)` at `s = 1` is equal to 1 expressed using `tsum` and for a
real variable. -/
theorem tendsto_sub_mul_tsum_nat_rpow :
    Tendsto (fun s : â„ â†¦ (s - 1) * âˆ‘' (n : â„•), 1 / (n : â„) ^ s) (ğ“[>] 1) (ğ“ 1) := by
  rw [â† tendsto_ofReal_iff, ofReal_one]
  have : Tendsto (fun s : â„ â†¦ (s : â„‚)) (ğ“[>] 1) (ğ“[{s | 1 < re s}] 1) :=
    continuous_ofReal.continuousWithinAt.tendsto_nhdsWithin (fun _ _ â†¦ by aesop)
  apply (tendsto_sub_mul_tsum_nat_cpow.comp this).congr fun s â†¦ ?_
  simp only [one_div, Function.comp_apply, ofReal_mul, ofReal_sub, ofReal_one, ofReal_tsum,
    ofReal_inv, ofReal_cpow (Nat.cast_nonneg _), ofReal_natCast]
