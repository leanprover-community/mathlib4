/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/

import Mathlib.NumberTheory.LSeries.DirichletContinuation2
import Mathlib.NumberTheory.DirichletCharacter.GaussSum
import Mathlib.Analysis.SpecialFunctions.Complex.CircleAddChar

/-!
# Functional equations of Dirichlet L-functions

We show that the completed `L`-functions of Dirichlet characters satisfy functional equations
relating `s` and `1 - s`.

## Brief outline of construction

We define a function `dualLFunction Ï‡ s`, and a variant `completedDualLFunction Ï‡ s` given
by multiplying by a Gamma factor in the usual way. We show that
- `completedLFunction Ï‡ (1 - s) = â–¡ * completedDualLFunction Ï‡ s` for an explicit factor `â–¡`,
- `dualLFunction Ï‡ s` is the analytic continuation of the Dirichlet series with coefficients
  `gaussSum Ï‡ (stdAddChar.mulShift n)`.

These results are valid for all `Ï‡`. However, if `Ï‡` is primitive, then we have
`gaussSum Ï‡ (stdAddChar.mulShift n) = gaussSum Ï‡ stdAddChar * Ï‡â»Â¹ n` and we recover the usual
formulation of the functional equation.

## Main definitions and results

All definitions and theorems are in the `DirichletCharacter` namespace.

* `DirichletCharacter.rootNumber`: the global root number of the L-series of `Ï‡`.
* `DirichletCharacter.IsPrimitive.completedLFunction_one_sub`: if `Ï‡` is primitive modulo `N`, then
  `completedLFunction Ï‡ s = N ^ (s - 1 / 2) * rootNumber Ï‡ * completedLFunction Ï‡â»Â¹ s`.
-/

open HurwitzZeta Complex ZMod Finset Classical DirichletCharacter

open scoped Real

namespace DirichletContinuationOld

open DirichletContinuationOld.DirichletCharacter

variable {N : â„•} [NeZero N]

local notation "G" Ï‡:max => gaussSum Ï‡ stdAddChar

/-- Global root number of `Ï‡` (for `Ï‡` primitive; junk otherwise). Defined as
`gaussSum Ï‡ stdAddChar / I ^ a / N ^ (1 / 2)`, where `a = 0` if even, `a = 1` if odd. -/
noncomputable def rootNumber (Ï‡ : DirichletCharacter â„‚ N) : â„‚ :=
  G Ï‡ / I ^ (if Ï‡.Even then 0 else 1) / N ^ (1 / 2 : â„‚)

lemma gaussSum_mod_one {R : Type*} [CommRing R] (Ï‡ : DirichletCharacter R 1)
    (e : AddChar (ZMod 1) R) :
    gaussSum Ï‡ e = 1 := by
  rw [gaussSum, univ_unique, sum_singleton]
  change Ï‡ 1 * e 0 = 1
  rw [map_one, AddChar.map_zero_eq_one, one_mul]

lemma gammaFactor_inv {N : â„•} (Ï‡ : DirichletCharacter â„‚ N) :
    gammaFactor Ï‡â»Â¹ = gammaFactor Ï‡ := by
  unfold gammaFactor
  rw [show Ï‡â»Â¹.Even â†” Ï‡.Even by
    simp only [DirichletCharacter.Even, Ï‡.inv_apply_eq_inv', inv_eq_one]]

/-- "Dual" L-function of a Dirichlet character.

This is a meromorphic function which we show is equal to `L Ï‡ (1 - s)` up to the usual Gamma
factors, and is also equal to the sum of the Dirichlet series with `n`-th term
`gaussSum Ï‡ (stdAddChar.mulShift n)`.

When `Ï‡` is primitive we shall show that this is equal to `L Ï‡â»Â¹ s` (but the
definition is valid more generally). -/
noncomputable def dualLFunction (Ï‡ : DirichletCharacter â„‚ N) (s : â„‚) : â„‚ :=
  âˆ‘ j : ZMod N, Ï‡ j * expZeta (toAddCircle j) s

lemma dualLFunction_mod_one (Ï‡ : DirichletCharacter â„‚ (1 : â„•+)) :
    dualLFunction Ï‡ = riemannZeta := by
  ext s : 1
  simp only [dualLFunction, PNat.val_ofNat, univ_unique, sum_singleton]
  change Ï‡ 1 * expZeta (toAddCircle 0) s = _
  rw [map_one, map_zero, expZeta_zero, one_mul]

/-- Alternate version of the definition of `dualLFunction Ï‡ s` using sine / cosine zeta functions.
Useful for comparison with `completedDualLFunction Ï‡ s`. -/
lemma dualLFunction_def_signed (Ï‡ : DirichletCharacter â„‚ N) (s : â„‚) :
    dualLFunction Ï‡ s =
      if Ï‡.Even then âˆ‘ j : ZMod N, Ï‡ j * cosZeta (toAddCircle j) s
        else âˆ‘ j : ZMod N, Ï‡ j * I * sinZeta (toAddCircle j) s := by
  simp only [dualLFunction, â† mul_ite, expZeta, mul_add, sum_add_distrib]
  rcases Ï‡.even_or_odd with h | h
  Â· simp only [h, â†“reduceIte, add_right_eq_self, â† neg_eq_self â„‚, â† sum_neg_distrib]
    refine Fintype.sum_equiv (.neg _) _ _ fun i â†¦ ?_
    simp only [Equiv.neg_apply, map_neg, sinZeta_neg, mul_neg]
    rw [â† neg_one_mul i, map_mul, h, one_mul]
  Â· simp only [h.not_even, â†“reduceIte, add_left_eq_self, â† neg_eq_self â„‚,
      â† sum_neg_distrib, â† mul_assoc]
    refine Fintype.sum_equiv (.neg _) _ _ fun i â†¦ ?_
    simp only [Equiv.neg_apply, map_neg, cosZeta_neg, mul_neg]
    rw [â† neg_one_mul i, map_mul, h, neg_mul, neg_mul, one_mul]

/-- Completed version of `dualLFunction Ï‡ s`. -/
noncomputable def completedDualLFunction (Ï‡ : DirichletCharacter â„‚ N) (s : â„‚) : â„‚ :=
  if Ï‡.Even then âˆ‘ j : ZMod N, Ï‡ j * completedCosZeta (toAddCircle j) s
    else âˆ‘ j : ZMod N, Ï‡ j * I * completedSinZeta (toAddCircle j) s

lemma completedDualLFunction_mod_one (Ï‡ : DirichletCharacter â„‚ (1 : â„•+)) :
    completedDualLFunction Ï‡ = completedRiemannZeta := by
  ext s : 1
  simp only [completedDualLFunction, PNat.val_ofNat, univ_unique, sum_singleton,
    (show Ï‡.Even from map_one _), â†“reduceIte]
  change Ï‡ 1 * completedCosZeta (toAddCircle 0) s = _
  rw [map_one, map_zero, completedCosZeta_zero, one_mul]

lemma dualLFunction_eq_completed_div_gammaFactor (Ï‡ : DirichletCharacter â„‚ N) (s : â„‚)
    (h : s â‰  0 âˆ¨ Ï‡ â‰  1) :
    dualLFunction Ï‡ s = completedDualLFunction Ï‡ s / gammaFactor Ï‡ s := by
  rw [dualLFunction_def_signed, completedDualLFunction, gammaFactor]
  split_ifs with hÏ‡ <;> simp only [sum_div, mul_div_assoc, cosZeta, sinZeta]
  rcases eq_or_ne s 0 with rfl | hs
  Â· simp only [Function.update_same, Gammaâ„, zero_div, Gamma_zero, mul_zero, div_zero,
      sum_const_zero, â† sum_mul]
    rw [Ï‡.sum_eq_zero_of_ne_one (by tauto), zero_mul]
  Â· simp only [Function.update_noteq (by tauto)]

/-- General form of functional equation (allowing non-primitive characters): equality between
`completedLFunction` at `1 - s` and `completedDualLFunction` at `s`.

See `IsPrimitive.completedLFunction_one_sub` for the more familiar form when `Ï‡` is primitive. -/
lemma completedLFunction_one_sub_eq_dual (Ï‡ : DirichletCharacter â„‚ N) (s : â„‚) :
    completedLFunction Ï‡ (1 - s) =
      N ^ (s - 1) / I ^ (if Ï‡.Even then 0 else 1) * completedDualLFunction Ï‡ s := by
  rw [completedLFunction, completedDualLFunction, neg_sub]
  split_ifs
  Â· simp only [completedHurwitzZetaEven_one_sub, pow_zero, div_one]
  Â· rw [pow_one, div_mul_eq_mul_div, mul_div_assoc, sum_div]
    congr 2 with i
    rw [mul_right_comm, mul_div_cancel_rightâ‚€ _ I_ne_zero, completedHurwitzZetaOdd_one_sub]

/-- If `Ï‡ â‰  1` then the dual completed L-function is differentiable everywhere. -/
lemma differentiable_completedDualLFunction {Ï‡ : DirichletCharacter â„‚ N} (hÏ‡ : Ï‡ â‰  1) :
    Differentiable â„‚ (completedDualLFunction Ï‡) := by
  unfold completedDualLFunction
  split_ifs
  Â· simp only [completedCosZeta_eq, mul_sub, sum_sub_distrib]
    refine (Differentiable.sub ?_ ?_).sub ?_
    Â· exact Differentiable.sum fun i _ â†¦ (differentiable_completedCosZetaâ‚€ _).const_mul _
    Â· simp only [â† sum_mul, Ï‡.sum_eq_zero_of_ne_one hÏ‡, zero_mul, differentiable_const]
    Â· simp only [div_eq_mul_inv, ite_mul, mul_ite]
      have (a : ZMod N) : toAddCircle a = 0 â†” a = 0 := by
        simpa only [â† (toAddCircle (N := N)).map_zero] using (toAddCircle_injective _).eq_iff
      have h : N â‰  1 := (show Ï‡ â‰  1 by tauto) âˆ˜ level_one' Ï‡
      simp only [this, â† div_eq_mul_inv, zero_div, mul_zero, sum_ite_eq', mem_univ, â†“reduceIte,
        map_zero' _ h, zero_mul, differentiable_const]
  Â· exact Differentiable.sum fun i _ â†¦ (differentiable_completedSinZeta _).const_mul _

/-- If the modulus is `â‰  1`, then `dualLFunction Ï‡` is differentiable. -/
lemma differentiable_dualLFunction (Ï‡ : DirichletCharacter â„‚ N) (hN : N â‰  1) :
    Differentiable â„‚ (dualLFunction Ï‡) := by
  unfold dualLFunction
  refine Differentiable.sum fun j _ â†¦ ?_
  rcases eq_or_ne j 0 with rfl | hj
  Â· simp only [Ï‡.map_zero' hN, zero_mul, differentiable_const]
  Â· apply Differentiable.const_mul
    refine differentiable_expZeta_of_ne_zero fun h â†¦ hj ?_
    rwa [â† (toAddCircle (N := N)).map_zero, (toAddCircle_injective _).eq_iff] at h

/--
On the half-space `1 < re s`, the dual L-function is given by the Dirichlet series with
coefficients `gaussSum Ï‡ (stdAddChar.mulShift n)`.
-/
lemma dualLFunction_eq_LSeries_gaussSum (Ï‡ : DirichletCharacter â„‚ N) {s : â„‚} (hs : 1 < re s) :
    dualLFunction Ï‡ s = LSeries (fun n â†¦ gaussSum Ï‡ (stdAddChar.mulShift n)) s := by
  rw [dualLFunction]
  have (a : â„) := (LSeriesHasSum_exp a hs).LSeries_eq
  simp only [toAddCircle_apply, â† this, LSeries, â† tsum_mul_left]
  rw [â† tsum_sum (fun i _ â†¦ Summable.mul_left _ (LSeriesHasSum_exp _ hs).LSeriesSummable)]
  congr 1 with n
  simp only [LSeries.term_of_ne_zero' (ne_zero_of_one_lt_re hs), â† mul_div_assoc, â† sum_div]
  congr 2 with i
  rw [AddChar.mulShift_apply, mul_comm _ (n : â„‚), Complex.exp_nat_mul, â† nsmul_eq_mul,
    AddChar.map_nsmul_eq_pow]
  simp only [stdAddChar_apply, toCircle_apply, push_cast, mul_div_assoc]

namespace IsPrimitive

/--
First step towards functional equation: prove equality of dual L-series with L-series of `Ï‡â»Â¹`
in convergence range. Private because it is superseded by `dualLFunction_eq` below.
-/
private lemma dualLFunction_eq_of_one_lt {Ï‡ : DirichletCharacter â„‚ N} (hÏ‡ : IsPrimitive Ï‡)
    {s : â„‚} (hs : 1 < re s) :
    dualLFunction Ï‡ s = G Ï‡ * LFunction Ï‡â»Â¹ s := by
  simp only [dualLFunction_eq_LSeries_gaussSum _ hs, LSeries, LSeries.term_of_ne_zero'
    (ne_zero_of_one_lt_re hs), LFunction_eq_LSeries _ hs, â† tsum_mul_left]
  congr 1 with n
  simp only [â† mul_div_assoc, gaussSum_mulShift_of_isPrimitive _ hÏ‡, mul_comm]

/--
Second step towards functional equation: prove equality of completed dual L-series with completed
L-series of `Ï‡â»Â¹` in convergence range. Private because it is superseded by
`completedDualLFunction_eq` below.
-/
private lemma completedDualLFunction_eq_of_one_lt {Ï‡ : DirichletCharacter â„‚ N} (hÏ‡ : IsPrimitive Ï‡)
    {s : â„‚} (hs : 1 < re s) :
    completedDualLFunction Ï‡ s = G Ï‡ * completedLFunction Ï‡â»Â¹ s := by
  have h := dualLFunction_eq_of_one_lt hÏ‡ hs
  suffices dualLFunction Ï‡ s = completedDualLFunction Ï‡ s / gammaFactor Ï‡ s by
    rwa [this, LFunction_eq_completed_div_gammaFactor _ _ (Or.inl
      (Complex.ne_zero_of_one_lt_re hs)), â† mul_div_assoc, gammaFactor_inv, div_left_inj'] at h
    rw [gammaFactor] -- remains to show gammaFactor â‰  0
    split_ifs <;>
    apply Gammaâ„_ne_zero_of_re_pos <;>
    [skip; rw [add_re, one_re]] <;>
    positivity
  exact dualLFunction_eq_completed_div_gammaFactor _ _ (Or.inl (Complex.ne_zero_of_one_lt_re hs))

/--
The completed dual L-function of a primitive character is the completed L-function of its inverse.
-/
lemma completedDualLFunction_eq {Ï‡ : DirichletCharacter â„‚ N} (hÏ‡ : IsPrimitive Ï‡) (s : â„‚) :
    completedDualLFunction Ï‡ s = G Ï‡ * completedLFunction Ï‡â»Â¹ s := by
  rcases eq_or_ne Ï‡ 1 with rfl | hÏ‡'
  Â· obtain rfl : N = 1 := by
      rwa [IsPrimitive, conductor_one (NeZero.ne _), Eq.comm] at hÏ‡
    rw [inv_one, completedLFunction_mod_one, completedDualLFunction_mod_one, gaussSum_mod_one,
      one_mul]
  apply congr_fun
  apply AnalyticOn.eq_of_eventuallyEq (ğ•œ := â„‚)
  Â· exact (differentiable_completedDualLFunction hÏ‡').differentiableOn.analyticOn isOpen_univ
  Â· refine (Differentiable.differentiableOn ?_).analyticOn isOpen_univ
    exact (differentiable_completedLFunction (inv_ne_one.mpr hÏ‡')).const_mul _
  Â· refine Filter.eventually_of_mem ?_ (fun t ht â†¦ completedDualLFunction_eq_of_one_lt hÏ‡ ht)
    refine (continuous_re.isOpen_preimage _ (isOpen_lt' _)).mem_nhds (?_ : 1 < re (2 : â„‚))
    simp only [re_ofNat, Nat.one_lt_ofNat]

/--
The dual L-function of a primitive character is the L-function of its inverse.
-/
lemma dualLFunction_eq {Ï‡ : DirichletCharacter â„‚ N} (hÏ‡ : IsPrimitive Ï‡) (s : â„‚) :
    dualLFunction Ï‡ s = G Ï‡ * LFunction Ï‡â»Â¹ s := by
  rcases eq_or_ne Ï‡ 1 with rfl | hÏ‡'
  Â· obtain âŸ¨rflâŸ© : N = 1 := by
      rwa [isPrimitive_def, conductor_one (NeZero.ne _), Eq.comm] at hÏ‡
    rw [inv_one, LFunction_mod_one, dualLFunction_mod_one, gaussSum_mod_one, one_mul]
  have : N â‰  1 := hÏ‡' âˆ˜ level_one' Ï‡
  rw [dualLFunction_eq_completed_div_gammaFactor _ _ (Or.inr hÏ‡'),
    LFunction_eq_completed_div_gammaFactor _ _  (Or.inr this),
    completedDualLFunction_eq hÏ‡, gammaFactor_inv, mul_div_assoc]

/-- Functional equation for primitive Dirichlet L-functions. -/
theorem completedLFunction_one_sub {Ï‡ : DirichletCharacter â„‚ N} (hÏ‡ : IsPrimitive Ï‡) (s : â„‚) :
    completedLFunction Ï‡ (1 - s) = N ^ (s - 1 / 2) * rootNumber Ï‡ * completedLFunction Ï‡â»Â¹ s := by
  simp only [completedLFunction_one_sub_eq_dual, rootNumber, completedDualLFunction_eq hÏ‡ s]
  suffices N ^ (s - 1) = (N : â„‚) ^ (s - 1 / 2 : â„‚) / N ^ (1 / 2 : â„‚) by rw [this]; ring
  rw [â† cpow_sub _ _ (NeZero.ne _), sub_sub, â† add_div, one_add_one_eq_two, div_self two_ne_zero]

end IsPrimitive

end DirichletContinuationOld
