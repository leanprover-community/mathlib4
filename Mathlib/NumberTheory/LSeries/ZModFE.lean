/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/

import Mathlib.NumberTheory.LSeries.ZMod
import Mathlib.Analysis.Convex.Topology
import Mathlib.LinearAlgebra.Dimension.DivisionRing
import Mathlib.Topology.Algebra.Module.Cardinality

/-!
# Functional equations for L-functions on `ZMod N`
-/

open HurwitzZeta Complex ZMod Finset Classical

open scoped Real

namespace ZMod

variable {N : â„•} [NeZero N]

lemma gammaFactor_dft {Î¦ : ZMod N â†’ â„‚} : gammaFactor (ğ“• Î¦) = gammaFactor Î¦ :=
  funext fun s â†¦ by simp only [gammaFactor, â† dft_even]

/-- The `L`-function of the Fourier transform of `Î¦`. -/
noncomputable def dualLFunction (Î¦ : ZMod N â†’ â„‚) (s : â„‚) : â„‚ :=
  âˆ‘ j : ZMod N, Î¦ j * expZeta (toAddCircle (-j)) s

/-- Completed version of `dualLFunction Î¦ s`. -/
noncomputable def completedDualLFunction (Î¦ : ZMod N â†’ â„‚) (s : â„‚) : â„‚ :=
  if Î¦.Even then âˆ‘ j : ZMod N, Î¦ j * completedCosZeta (toAddCircle j) s
    else âˆ‘ j : ZMod N, Î¦ j / I * completedSinZeta (toAddCircle j) s

lemma dualLFunction_def_signed {Î¦ : ZMod N â†’ â„‚} (hÎ¦ : Î¦.Even âˆ¨ Î¦.Odd) (s : â„‚) :
    dualLFunction Î¦ s =
      if Î¦.Even then âˆ‘ j : ZMod N, Î¦ j * cosZeta (toAddCircle j) s
        else âˆ‘ j : ZMod N, Î¦ j / I * sinZeta (toAddCircle j) s := by
  simp only [dualLFunction, â† mul_ite, expZeta, mul_add, sum_add_distrib]
  by_cases h : Î¦.Even
  Â· simp only [map_neg, cosZeta_neg, sinZeta_neg, h, â†“reduceIte, add_right_eq_self, â†
    neg_eq_self â„‚, â† sum_neg_distrib]
    refine Fintype.sum_equiv (.neg _) _ _ fun i â†¦ ?_
    simp only [mul_neg, Equiv.neg_apply, h i, map_neg, sinZeta_neg]
  Â· have h' : Î¦.Odd := by tauto
    simp only [map_neg, cosZeta_neg, sinZeta_neg, mul_neg, â† mul_assoc, â† sum_neg_distrib, h,
      â†“reduceIte, div_I, neg_mul, add_left_eq_self, â† neg_eq_self â„‚]
    refine Fintype.sum_equiv (.neg _) _ _ fun i â†¦ ?_
    simp only [Equiv.neg_apply, h' i, map_neg, cosZeta_neg, neg_mul]

lemma dualLFunction_eq_completed_div_gammaFactor {Î¦ : ZMod N â†’ â„‚} (hÎ¦ : Î¦.Even âˆ¨ Î¦.Odd) (s : â„‚)
    (h : s â‰  0 âˆ¨ âˆ‘ i, Î¦ i = 0) :
    dualLFunction Î¦ s = completedDualLFunction Î¦ s / gammaFactor Î¦ s := by
  rw [dualLFunction_def_signed hÎ¦, completedDualLFunction, gammaFactor]
  split_ifs with h' <;> simp only [sum_div, mul_div_assoc, cosZeta, sinZeta]
  rcases eq_or_ne s 0 with rfl | hs
  Â· simp only [Function.update_same, Gammaâ„, zero_div, Gamma_zero, mul_zero, div_zero,
      sum_const_zero, â† sum_mul, h.neg_resolve_left rfl, zero_mul]
  Â· simp only [Function.update_noteq (by tauto)]

/-- General form of functional equation (allowing non-primitive characters): equality between
`completedLFunction` at `1 - s` and `completedDualLFunction` at `s`. -/
lemma completedLFunction_one_sub_eq_dual (Î¦ : ZMod N â†’ â„‚) (s : â„‚) :
    completedLFunction Î¦ (1 - s) =
      N ^ (s - 1) * I ^ (if Î¦.Even then 0 else 1) * completedDualLFunction Î¦ s := by
  rw [completedLFunction, completedDualLFunction, neg_sub]
  split_ifs
  Â· simp only [completedHurwitzZetaEven_one_sub, pow_zero, mul_one]
  Â· simp only [completedHurwitzZetaOdd_one_sub, pow_one, mul_assoc, mul_sum _ _ I, â† mul_assoc I,
      mul_div_cancelâ‚€ _ I_ne_zero]

/--
If `Î¦ 0 = 0` and `âˆ‘ i, Î¦ i = 0`, then the dual completed L-function is differentiable everywhere.
-/
lemma differentiableAt_completedDualLFunction (Î¦ : ZMod N â†’ â„‚) (s : â„‚)
    (hÎ¦s : s â‰  0 âˆ¨ âˆ‘ i, Î¦ i = 0) (hÎ¦s' : s â‰  1 âˆ¨ Î¦ 0 = 0) :
    DifferentiableAt â„‚ (completedDualLFunction Î¦) s := by
  unfold completedDualLFunction
  split_ifs
  Â· simp only [completedCosZeta_eq, mul_sub, sum_sub_distrib]
    refine (DifferentiableAt.sub ?_ ?_).sub ?_
    Â· exact (Differentiable.sum fun i _ â†¦ (differentiable_completedCosZetaâ‚€ _).const_mul _) s
    Â· rcases hÎ¦s with h | h
      Â· simp only [mul_one_div, â† sum_div]
        exact (differentiableAt_const _).div differentiableAt_id h
      Â· simp only [â† sum_mul, h, zero_mul, differentiableAt_const]
    Â· rcases hÎ¦s' with h | h
      Â· simp only [â† mul_div_assoc, â† sum_div]
        refine (differentiableAt_const _).div ?_ ?_
        Â· exact (differentiableAt_const _).sub differentiableAt_id
        Â· rwa [sub_ne_zero, ne_comm]
      Â· simp only [toAddCircle_eq_zero, div_eq_mul_inv, ite_mul, one_mul, zero_mul, mul_ite,
          mul_zero, sum_ite_eq', mem_univ, â†“reduceIte, h, differentiableAt_const]
  Â· exact (Differentiable.sum fun i _ â†¦ (differentiable_completedSinZeta _).const_mul _) _

/-- If the modulus is `â‰  1`, then `dualLFunction Î¦` is differentiable. -/
lemma differentiable_dualLFunction {Î¦ : ZMod N â†’ â„‚} (hÎ¦' : Î¦ 0 = 0) :
    Differentiable â„‚ (dualLFunction Î¦) := by
  refine Differentiable.sum fun j _ â†¦ ?_
  rcases eq_or_ne j 0 with rfl | hj
  Â· simp only [hÎ¦', zero_mul, differentiable_const]
  Â· refine (differentiable_expZeta_of_ne_zero fun h â†¦ hj ?_).const_mul _
    rwa [â† (toAddCircle (N := N)).map_zero, (toAddCircle_injective _).eq_iff, neg_eq_zero] at h

/--
On the half-space `1 < re s`, the dual L-function of `Î¦` is equal to the L-function of `ğ“• Î¦ (-x).
-/
lemma dualLFunction_eq_LSeries_dft (Î¦ : ZMod N â†’ â„‚) {s : â„‚} (hs : 1 < re s) :
    dualLFunction Î¦ s = LSeries (ğ“• Î¦ Â·) s := by
  rw [dualLFunction, dft_def]
  simp only [toAddCircle_apply, â† fun a â†¦ (LSeriesHasSum_exp a hs).LSeries_eq, LSeries,
    â† tsum_mul_left]
  rw [â† tsum_sum (fun i _ â†¦ Summable.mul_left _ (LSeriesHasSum_exp _ hs).LSeriesSummable)]
  congr 1 with n
  simp only [LSeries.term_of_ne_zero' (ne_zero_of_one_lt_re hs), â† mul_div_assoc, â† sum_div,
    Submonoid.smul_def, smul_eq_mul, mul_comm _ (Î¦ _), mul_neg, neg_neg]
  congr 2 with i
  rw [mul_comm _ (n : â„‚), exp_nat_mul, ofReal_div, ofReal_natCast, ofReal_natCast,
    â† mul_div_assoc, â† toCircle_apply, mul_comm _ (n : ZMod N), â† nsmul_eq_mul, â† neg_nsmul,
    AddChar.map_nsmul_eq_pow, stdAddChar_apply]

/--
First step towards functional equation: prove equality of dual L-series with L-series of `Î¦â»Â¹`
in convergence range. Private because it is superseded by `dualLFunction_eq` below.
-/
private lemma dualLFunction_eq_of_one_lt (Î¦ : ZMod N â†’ â„‚) {s : â„‚} (hs : 1 < re s) :
    dualLFunction Î¦ s = LFunction (ğ“• Î¦) s := by
  simp only [dualLFunction_eq_LSeries_dft _ hs, LFunction_eq_LSeries _ hs]

/--
Second step towards functional equation: prove equality of completed dual L-series with completed
L-series of `Î¦â»Â¹` in convergence range. Private because it is superseded by
`completedDualLFunction_eq` below.
-/
private lemma completedDualLFunction_eq_of_one_lt {Î¦ : ZMod N â†’ â„‚} (hÎ¦ : Î¦.Even âˆ¨ Î¦.Odd)
    {s : â„‚} (hs : 1 < re s) :
    completedDualLFunction Î¦ s = completedLFunction (ğ“• Î¦) s := by
  have h := dualLFunction_eq_of_one_lt Î¦ hs
  rwa [dualLFunction_eq_completed_div_gammaFactor hÎ¦, LFunction_eq_completed_div_gammaFactor,
    gammaFactor_dft, div_left_inj'] at h
  Â· rw [gammaFactor] -- remains to show gammaFactor â‰  0
    split_ifs <;>
    apply Gammaâ„_ne_zero_of_re_pos <;>
    [skip; rw [add_re, one_re]] <;>
    positivity
  Â· simpa only [â† dft_even, â† dft_odd] using hÎ¦
  all_goals exact Or.inl (Complex.ne_zero_of_one_lt_re hs)

/--
The completed dual L-function of `Î¦` is the completed L-function of its Fourier transform.
-/
lemma completedDualLFunction_eq {Î¦ : ZMod N â†’ â„‚} (hÎ¦ : Î¦.Even âˆ¨ Î¦.Odd)
    (s : â„‚) (hÎ¦s : s â‰  0 âˆ¨ âˆ‘ i, Î¦ i = 0) (hÎ¦s' : s â‰  1 âˆ¨ Î¦ 0 = 0) :
    completedDualLFunction Î¦ s = completedLFunction (ğ“• Î¦) s := by
  revert s
  let U : Set â„‚ := fun s â†¦ (s â‰  0 âˆ¨ âˆ‘ i, Î¦ i = 0) âˆ§ (s â‰  1 âˆ¨ Î¦ 0 = 0)
  have hUo : IsOpen U := by
    apply IsOpen.inter
    Â· by_cases h : âˆ‘ i, Î¦ i = 0
      Â· simpa only [eq_true_intro h, or_true] using isOpen_univ
      Â· simpa only [eq_false_intro h, or_false] using isOpen_compl_iff.mpr isClosed_singleton
    Â· by_cases h : Î¦ 0 = 0
      Â· simpa only [eq_true_intro h, or_true] using isOpen_univ
      Â· simpa only [eq_false_intro h, or_false] using isOpen_compl_iff.mpr isClosed_singleton
  suffices U.EqOn _ _ from fun s hs hs' â†¦ this <| Set.mem_setOf.mpr âŸ¨hs, hs'âŸ©
  apply AnalyticOn.eqOn_of_preconnected_of_eventuallyEq (ğ•œ := â„‚) (zâ‚€ := 2)
  Â· constructor <;> norm_num
  Â· apply Filter.eventuallyEq_of_mem ?_ fun s hs â†¦ completedDualLFunction_eq_of_one_lt hÎ¦ hs
    Â· apply (continuous_re.isOpen_preimage _ isOpen_Ioi).mem_nhds
      simp only [Set.mem_preimage, re_ofNat, Set.mem_Ioi, Nat.one_lt_ofNat]
  Â· refine DifferentiableOn.analyticOn (fun s hs â†¦ ?_) hUo
    exact (differentiableAt_completedDualLFunction _ _ hs.1 hs.2).differentiableWithinAt
  Â· refine DifferentiableOn.analyticOn (fun s hs â†¦ ?_) hUo
    refine (differentiableAt_completedLFunction ?_ s ?_ ?_).differentiableWithinAt
    Â· rwa [â† dft_even, â† dft_odd]
    Â· simp only [dft_def, mul_zero, neg_zero, AddChar.map_zero_eq_one, one_smul]
      exact hs.1
    Â· refine hs.2.imp (by tauto) (fun hÎ¦ â†¦ ?_)
      have := congr_fun (dft_dft Î¦) 0
      rw [neg_zero, hÎ¦, smul_zero, dft_def] at this
      simpa only [mul_zero, neg_zero, AddChar.map_zero_eq_one, one_smul]
  Â· change IsPreconnected ({s | (s â‰  0 âˆ¨ âˆ‘ i : ZMod N, Î¦ i = 0)} âˆ© {s  | s â‰  1 âˆ¨ Î¦ 0 = 0})
    suffices IsPreconnected ({s : â„‚ | s = 0 âˆ§ âˆ‘ i : ZMod N, Î¦ i â‰  0} âˆª
        {s : â„‚ | s = 1 âˆ§ Î¦ 0 â‰  0})á¶œ by
      rw [Set.compl_union, Set.compl_def, Set.compl_def] at this
      convert this using 1
      refine Set.ext fun x â†¦ ?_
      simp only [Set.mem_setOf, not_and_or, ne_eq, not_not]
    apply IsConnected.isPreconnected
    apply Set.Countable.isConnected_compl_of_one_lt_rank (by simp)
    apply Set.Countable.union
    Â· apply Set.Countable.mono (sâ‚‚ := {0}) <;> simp
    Â· apply Set.Countable.mono (sâ‚‚ := {1}) <;> simp

/--
The dual L-function of a primitive character is the L-function of its inverse.
-/
lemma dualLFunction_eq {Î¦ : ZMod N â†’ â„‚} (hÎ¦ : Î¦.Even âˆ¨ Î¦.Odd) (s : â„‚)
    (hÎ¦s : s â‰  0 âˆ¨ âˆ‘ i, Î¦ i = 0) (hÎ¦s' : s â‰  1 âˆ¨ Î¦ 0 = 0) :
    dualLFunction Î¦ s = LFunction (ğ“• Î¦) s := by
  rw [dualLFunction_eq_completed_div_gammaFactor hÎ¦ _ hÎ¦s,
    LFunction_eq_completed_div_gammaFactor (by simpa [â† dft_odd, â† dft_even] using hÎ¦),
    completedDualLFunction_eq hÎ¦ _ hÎ¦s hÎ¦s', gammaFactor_dft]
  simp only [dft_def, mul_zero, neg_zero, AddChar.map_zero_eq_one, one_smul]
  exact hÎ¦s

lemma completedLFunction_one_sub_eq {Î¦ : ZMod N â†’ â„‚} (hÎ¦ : Î¦.Even âˆ¨ Î¦.Odd)
    (s : â„‚) (hÎ¦s : s â‰  0 âˆ¨ âˆ‘ i, Î¦ i = 0) (hÎ¦s' : s â‰  1 âˆ¨ Î¦ 0 = 0) :
    completedLFunction Î¦ (1 - s) =
      â†‘N ^ (s - 1) * I ^ (if Function.Even Î¦ then 0 else 1) * completedLFunction (ğ“• Î¦) s := by
  rw [completedLFunction_one_sub_eq_dual, completedDualLFunction_eq hÎ¦ _ hÎ¦s hÎ¦s', mul_assoc]

end ZMod
