/-
Copyright (c) 2024 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.NumberTheory.LSeries.Deriv
import Mathlib.NumberTheory.LSeries.RiemannZeta

/-!
# L-series of counting functions

Let `F : Î± â†’ â„•` be a function. Assume that there exists a positive real `l` such that
`#{x | F x â‰¤ n} / n' tends to `l`. In this file, we prove results on the L-series defined by the
function `n â†¦ Nat.card {x | F x = n}`.

## Main results.

* `NumberTheory.LSeriesSummable_card_eq`: the L-series defined by `n â†¦ Nat.card {x | F x = n}`
  is summable for `s âˆˆ â„‚` with `1 < s.re`.

* `NumberTheory.LSeries.card_eq_residue` : the L-series defined by
  `n â†¦ Nat.card {x | F x = n}` has a simple pole at `s = 1` of residue `l`.

* `NumberTheory.LSeries.abscissaOfAbsConv_card_eq `: the L-series defined by
  `n â†¦ Nat.card {x | F x = n}` has abscissa of absolute convergence `1`.
-/

open Filter Topology Complex Metric LSeries

namespace NumberTheory.LSeries.card

variable {Î± : Type*} {F : Î± â†’ â„•} {l : â„}
  (hF : Tendsto (fun n â†¦ (Nat.card {x | F x â‰¤ n} : â„) / n) atTop (ğ“ l)) (hl : 0 < l)

section card_le

include hF hl

theorem finite_card_le (n : â„•) :
    {x | F x â‰¤ n}.Finite := by
  contrapose! hl
  have h_card : âˆ€ â¦ƒmâ¦„, n â‰¤ m â†’ Nat.card {x | F x â‰¤ m} = 0 :=
    fun _ h â†¦ Set.Nat.card_coe_set_eq _ â–¸ (Set.Infinite.mono (fun _ h' â†¦ h'.trans h) hl).ncard
  suffices Tendsto (fun n â†¦ (Nat.card {x | F x â‰¤ n} : â„) / n) atTop (ğ“ 0) by
    rw [tendsto_nhds_unique hF this]
  exact tendsto_atTop_of_eventually_const fun _ h â†¦ by rw [h_card h, Nat.cast_zero, zero_div]

theorem finite_card_eq (n : â„•) :
    {x | F x = n}.Finite :=
  (finite_card_le hF hl n).subset fun _ h â†¦ h.le

theorem card_eq_succ_add_card_le (n : â„•) :
    Nat.card {x | F x = n + 1} + Nat.card {x | F x â‰¤ n} = Nat.card {x | F x â‰¤ n + 1} := by
  classical
  have : âˆ€ n, Fintype {x | F x â‰¤ n} := fun _ â†¦ (finite_card_le hF hl _).fintype
  have : âˆ€ n, Fintype {x | F x = n} := fun _ â†¦ (finite_card_eq hF hl _).fintype
  rw [Nat.card_eq_card_toFinset, Nat.card_eq_card_toFinset, Nat.card_eq_card_toFinset,
    â† Finset.card_union_of_disjoint]
  Â· congr with x
    simpa [â† Nat.lt_add_one_iff (n := n)] using le_iff_eq_or_lt.symm
  Â· exact Finset.disjoint_left.mpr fun _ h â†¦ by simp_all

theorem card_le_not_bounded (N : â„•) :
    âˆƒ n, N â‰¤ Nat.card {x | F x â‰¤ n} := by
  contrapose! hl
  refine tendsto_le_of_eventuallyLE hF (tendsto_const_div_atTop_nhds_zero_nat (N : â„)) ?_
  filter_upwards with n using div_le_div_of_nonneg_right (Nat.cast_le.mpr (hl n).le) n.cast_nonneg

theorem mono_card_le :
    Monotone (Nat.card {x | F x â‰¤ Â·}) :=
  fun _ _ hâ‚ â†¦ Nat.card_mono (finite_card_le hF hl _) fun _ hâ‚‚ â†¦ hâ‚‚.trans hâ‚

end card_le

noncomputable section val

variable (F) in
/-- The sequence of values taken by `F` sorted by increasing order, see `card_val_eq_succ` and
`monotone_val`. -/
def val (k : â„•) : â„• := sInf {n : â„• | k â‰¤ Nat.card {x | F x â‰¤ n}}

include hl hF

theorem val_eq_succ_iff {k n : â„•} :
    val F k = n + 1 â†” Nat.card {x | F x â‰¤ n} < k âˆ§ k â‰¤ Nat.card {x | F x â‰¤ n + 1} := by
  rw [val, Nat.sInf_upward_closed_eq_succ_iff, Set.mem_setOf_eq, Set.mem_setOf_eq, not_le, and_comm]
  exact fun _ _ hâ‚ hâ‚‚ â†¦ hâ‚‚.trans (mono_card_le hF hl hâ‚)

/-- For `0 < n`, there are as many `k : â„•` such that `val k = n` than elements `x : Î±` such
that `F x = n`.-/
theorem card_val_eq_succ (n : â„•) : Nat.card {k | val F k = n + 1} = Nat.card {x | F x = n + 1} := by
  simp_rw [val_eq_succ_iff hF hl, â† Finset.mem_Ioc, Finset.setOf_mem, Nat.card_eq_card_toFinset,
    Finset.toFinset_coe, Nat.card_Ioc, (Nat.eq_sub_of_add_eq (card_eq_succ_add_card_le hF hl n))]

theorem val_not_bounded (n : â„•) :
    âˆƒ k, n â‰¤ val F k :=
  âŸ¨Nat.card {x | F x â‰¤ n} + 1,
    le_csInf (card_le_not_bounded hF hl _) fun _ h â†¦  ((mono_card_le hF hl).reflect_lt h).leâŸ©

theorem mono_val : Monotone (val F) :=
  fun _ _ h â†¦ le_csInf (card_le_not_bounded hF hl _)
    fun _ h' â†¦ csInf_le (OrderBot.bddBelow _) (h.trans h')

theorem tendsto_atTop_val : Tendsto (val F) atTop atTop :=
  Monotone.tendsto_atTop_atTop (mono_val hF hl) (val_not_bounded hF hl)

theorem finite_val_eq (n : â„•) :
    {k | val F k = n}.Finite := by
  rw [â† compl_mem_cofinite, show {k | val F k = n} = val F â»Â¹' {n} by rfl, â† Set.preimage_compl]
  exact (Nat.cofinite_eq_atTop â–¸ tendsto_atTop_val hF hl) (by simp)

/-- A consequence of `card_val_eq_succ` that is useful later on. -/
theorem card_eq_div_pow_eq_tsum_fiber (n : â„•) {s : â„} (hs : s â‰  0) :
    (Nat.card {x | F x = n}) / (n : â„) ^ s = âˆ‘' (k : â†‘(val F â»Â¹' {n})), 1 / (val F k : â„) ^ s := by
  have : Fintype {x | val F x = n} := (finite_val_eq hF hl n).fintype
  have : Fintype {x | F x = n} := (finite_card_eq hF hl n).fintype
  rw [â† Equiv.tsum_eq (Equiv.setCongr (by rfl : {k | val F k = n} = val F â»Â¹' {n})), tsum_fintype,
    Finset.sum_congr rfl (fun x _ â†¦ by rw [Equiv.setCongr_apply, x.prop]), Finset.sum_const,
    Finset.card_univ, nsmul_eq_mul, mul_one_div]
  cases n with
  | zero => simp only [Nat.cast_zero, Real.zero_rpow hs, div_zero]
  | succ n => rw [â† card_val_eq_succ hF hl n, â† Nat.card_eq_fintype_card]

private theorem tendsto_atTop_div_val_auxâ‚ :
    âˆ€á¶  k in atTop, (Nat.card {x | F x â‰¤ val F k - 1} : â„) / (val F k - 1) * (1 - 1 / val F k) â‰¤
      (k : â„) / (val F k) := by
  filter_upwards [(tendsto_atTop.mp (tendsto_atTop_val hF hl)) 2] with k hk
  rw [one_sub_div (by positivity), â† mul_div_assoc, div_mul_cancelâ‚€]
  Â· refine div_le_div_of_nonneg_right (Nat.cast_le.mpr (le_of_lt ?_)) (by positivity)
    simpa only [Set.coe_setOf, Set.mem_setOf_eq, not_le] using
      Nat.not_mem_of_lt_sInf (Nat.sub_one_lt_of_lt hk)
  Â· rw [sub_ne_zero, Nat.cast_ne_one]
    linarith

private theorem tendsto_atTop_div_val_auxâ‚‚ :
  âˆ€á¶  k : â„• in atTop, (k : â„) / (val F k) â‰¤
    (Nat.card {x | F x â‰¤ val F k + 1} : â„) / (val F k + 1) * (1 + 1 / val F k) := by
  filter_upwards [(tendsto_atTop.mp (tendsto_atTop_val hF hl)) 2] with k hk
  rw [one_add_div (by positivity), â† mul_div_assoc, div_mul_cancelâ‚€ _ (by positivity)]
  refine div_le_div_of_nonneg_right (Nat.cast_le.mpr ?_) (Nat.cast_nonneg _)
  refine le_trans ?_ (mono_card_le hF hl (Nat.le_succ _))
  exact Nat.sInf_mem (card_le_not_bounded hF hl k)

/-- The sequence `k â†¦ k / val k` also tends to `l` at top. -/
theorem tendsto_atTop_div_val : Tendsto (fun k : â„• â†¦ (k : â„) / (val F k)) atTop (ğ“ l) := by
  have h : Tendsto (fun k â†¦ 1 / (val F k : â„)) atTop (ğ“ 0) := by
    simp_rw [one_div]
    exact Tendsto.inv_tendsto_atTop <| tendsto_natCast_atTop_iff.mpr (tendsto_atTop_val hF hl)
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le' ?_ ?_
    (tendsto_atTop_div_val_auxâ‚ hF hl) (tendsto_atTop_div_val_auxâ‚‚ hF hl)
  Â· rw [show ğ“ l = ğ“ (l * (1 - 0)) by rw [sub_zero, mul_one]]
    refine Tendsto.mul ?_ (h.const_sub 1)
    refine (hF.comp <| (tendsto_sub_atTop_nat 1).comp (tendsto_atTop_val hF hl)).congr' ?_
    filter_upwards [(tendsto_atTop.mp (tendsto_atTop_val hF hl)) 1] with k hk
    simp_rw [Function.comp_apply, Nat.cast_pred hk]
  Â· rw [show ğ“ l = ğ“ (l * (1 + 0)) by rw [add_zero, mul_one]]
    refine Tendsto.mul ?_ (h.const_add 1)
    exact (hF.comp ((tendsto_add_atTop_nat 1).comp (tendsto_atTop_val hF hl))).congr fun _ â†¦ by simp

theorem eventually_lt_one_div_val_rpow_lt {Îµ : â„} (hÎµâ‚ : 0 < Îµ) (hÎµâ‚‚ : Îµ â‰¤ l) :
    âˆ€á¶  k : â„• in atTop, âˆ€ s : â„, 0 < s â†’ (l - Îµ) ^ s / (k : â„) ^ s < 1 / val F k ^ s âˆ§
      1 / val F k ^ s < (l + Îµ) ^ s / (k : â„) ^ s := by
  rw [â† sub_nonneg] at hÎµâ‚‚ -- To help positivity
  filter_upwards [eventually_gt_atTop 0, tendsto_nhds.mp (tendsto_atTop_div_val hF hl) Îµ hÎµâ‚]
    with n hn h s hs
  rwa [div_lt_iffâ‚€', lt_div_iffâ‚€', mul_one_div, â† Real.div_rpow, Real.rpow_lt_rpow_iff,
    Real.rpow_lt_rpow_iff, â† neg_add_lt_iff_lt_add, sub_eq_add_neg, â† lt_neg_add_iff_add_lt,
    neg_add_eq_sub, â† abs_lt]
  all_goals positivity

theorem summable_one_div_val_rpow {s : â„} (hs : 1 < s) :
    Summable (fun n â†¦ 1 / (val F n : â„) ^ s) := by
  refine summable_of_isBigO_nat' (Real.summable_one_div_nat_rpow.mpr hs)
    (Asymptotics.isBigO_iff.mpr âŸ¨(l + l) ^ s, ?_âŸ©)
  filter_upwards [eventually_lt_one_div_val_rpow_lt hF hl hl le_rfl] with k hk
  convert (hk s (zero_lt_one.trans hs)).2.le using 1
  Â· rw [Real.norm_eq_abs, _root_.abs_of_nonneg (by positivity)]
  Â· rw [Real.norm_eq_abs, _root_.abs_of_nonneg (by positivity), mul_one_div]

theorem card_eq_div_pow_eq_tsum (n : â„•) {s : â„} (hs : s â‰  0) :
    (Nat.card {x | F x = n}) / (n : â„) ^ s = âˆ‘' (k : â†‘(val F â»Â¹' {n})), 1 / (val F k : â„) ^ s := by
  have : Fintype {x | val F x = n} := (finite_val_eq hF hl n).fintype
  have : Fintype {x | F x = n} := (finite_card_eq hF hl n).fintype
  rw [â† Equiv.tsum_eq (Equiv.setCongr (by rfl : {k | val F k = n} = val F â»Â¹' {n})), tsum_fintype,
    Finset.sum_congr rfl (fun x _ â†¦ by rw [Equiv.setCongr_apply, x.prop]), Finset.sum_const,
    Finset.card_univ, nsmul_eq_mul, mul_one_div]
  cases n with
  | zero => simp only [Nat.cast_zero, Real.zero_rpow hs, div_zero]
  | succ n => rw [â† card_val_eq_succ hF hl n, â† Nat.card_eq_fintype_card]

theorem tsum_card_eq_div_rpow_eq_tsum :
    (fun s : â„ â†¦ âˆ‘' n, Nat.card {x | F x = n} / (n : â„) ^ s) =á¶ [ğ“[>] 1]
    (fun s â†¦ âˆ‘' k, 1 / (val F k : â„) ^ s) := by
  filter_upwards [eventually_mem_nhdsWithin] with s hs
  convert HasSum.tsum_eq (HasSum.tsum_fiberwise
    (Summable.hasSum (summable_one_div_val_rpow hF hl hs)) (val F)) with n
  exact card_eq_div_pow_eq_tsum hF hl n (zero_lt_one.trans hs).ne'

end val

section tendsto

theorem tendsto_sub_mul_sum_one_div_rpow (T : Finset â„•) (v : â„• â†’ â„•) :
    Tendsto (fun s : â„ â†¦ (s - 1) * âˆ‘ n âˆˆ T, 1 / (v n : â„) ^ s) (ğ“ 1) (ğ“ 0) := by
  simp_rw [Finset.mul_sum, one_div, show ğ“ (0 : â„) = ğ“ (âˆ‘ n âˆˆ T, 0 * 1 / (v n : â„) ^ (1 : â„)) by
    simp_rw [zero_mul, zero_div, Finset.sum_const, smul_zero]]
  refine tendsto_finset_sum _ fun n _ â†¦ ?_
  by_cases hv : v n = 0
  Â· simp_rw [hv, Nat.cast_zero, zero_mul, zero_div]
    refine tendsto_const_nhds.congr' ?_
    filter_upwards [eventually_ne_nhds one_ne_zero] with s hs
    rw [Real.zero_rpow hs, inv_zero, mul_zero]
  Â· refine Tendsto.mul ?_ ?_
    Â· convert (continuous_sub_right (1 : â„)).tendsto 1 using 2
      rw [zero_mul, sub_self]
    Â· convert (Real.continuousAt_const_rpow (Nat.cast_ne_zero.mpr hv)).tendsto.invâ‚€ ?_
      rwa [Real.rpow_one, Nat.cast_ne_zero]

theorem tendsto_rpow_mul_sub_mul_tsum_one_div_rpow {c : â„} (hc : c â‰  0) (T : Finset â„•) :
    Tendsto (fun s â†¦ c ^ s * (s - 1) *
      âˆ‘' n : â†‘((T : Set â„•)á¶œ), 1 / (n : â„) ^ s) (ğ“[>] 1) (ğ“ c) := by
  simp_rw [mul_assoc, show ğ“ c = ğ“ (c * (1 - 0)) by rw [sub_zero, mul_one]]
  refine Tendsto.mul (tendsto_nhdsWithin_of_tendsto_nhds ?_) ?_
  Â· refine Continuous.tendsto' ?_ 1 c (by rw [Real.rpow_one])
    exact continuous_const.rpow continuous_id fun _ â†¦ Or.inl hc
  Â· refine (tendsto_sub_mul_tsum_nat_rpow.sub (tendsto_nhdsWithin_of_tendsto_nhds
      (tendsto_sub_mul_sum_one_div_rpow T (fun n â†¦ n)))).congr' ?_
    filter_upwards [eventually_mem_nhdsWithin] with s hs
    rw [sub_eq_iff_eq_add', â† mul_add, sum_add_tsum_compl (Real.summable_one_div_nat_rpow.mpr hs)]

include hl hF

theorem exist_finset_lt_sub_mul_tsum_one_div_val_rpow_lt {Îµ : â„} (hÎµâ‚ : 0 < Îµ) (hÎµâ‚‚ : Îµ â‰¤ l) :
    âˆƒ T : Finset â„•, âˆ€ s, 1 < s â†’
      (s - 1) * âˆ‘ n âˆˆ T, 1 / (val F n : â„) ^ s +
        (l - Îµ) ^ s * (s - 1) * âˆ‘' n : â†‘((T : Set â„•)á¶œ), 1 / (n : â„) ^ s <
          (s - 1) * âˆ‘' n, 1 / (val F n : â„) ^ s âˆ§
      (s - 1) * âˆ‘' n, 1/ (val F n : â„) ^ s <
        (s - 1) * âˆ‘ n âˆˆ T, 1 / (val F n : â„) ^ s +
          (l + Îµ) ^ s * (s - 1) * âˆ‘' n : â†‘((T : Set â„•)á¶œ), 1 / (n : â„) ^ s := by
  obtain âŸ¨N, hNâŸ© := eventually_atTop.mp <| eventually_lt_one_div_val_rpow_lt hF hl hÎµâ‚ hÎµâ‚‚
  refine âŸ¨Finset.range N, fun s hs â†¦ ?_âŸ©
  simp_rw [â† sum_add_tsum_compl (s := Finset.range N) (summable_one_div_val_rpow hF hl hs), mul_add,
    add_lt_add_iff_left, mul_assoc, mul_left_comm _ (s- 1), mul_lt_mul_left (sub_pos.mpr hs),
    â† tsum_mul_left, mul_one_div]
  have hâ‚ : âˆ€ {S : Set â„•} {c : â„}, Summable fun n : S â†¦ c / (n : â„) ^ s :=
    ((Real.summable_nat_rpow_inv.mpr hs).mul_left _).subtype _
  have hâ‚‚ : âˆ€ {S : Set â„•}, Summable fun n : S â†¦ 1 / (val F n : â„) ^ s :=
    (summable_one_div_val_rpow hF hl hs).subtype _
  refine âŸ¨tsum_lt_tsum (i := âŸ¨N+1, by simpâŸ©) ?_ ?_ hâ‚ hâ‚‚,
    tsum_lt_tsum (i := âŸ¨N+1, by simpâŸ©) ?_ ?_ hâ‚‚ hâ‚ âŸ©
  Â· rintro âŸ¨i, hiâŸ©
    simp only [Finset.coe_range, Set.compl_Iio, Set.mem_Ici] at hi
    exact (hN i hi s (zero_lt_one.trans hs)).1.le
  Â· exact (hN (N + 1) (Nat.le_add_right N 1) s (zero_lt_one.trans hs)).1
  Â· rintro âŸ¨i, hiâŸ©
    simp only [Finset.coe_range, Set.compl_Iio, Set.mem_Ici] at hi
    exact (hN i hi s (zero_lt_one.trans hs)).2.le
  Â· exact (hN (N + 1) (Nat.le_add_right N 1) s (zero_lt_one.trans hs)).2

theorem tendsto_sub_mul_tsum_one_div_val_rpow :
    Tendsto (fun s : â„ â†¦ (s - 1) * âˆ‘' n, 1 / (val F n : â„) ^ s) (ğ“[>] 1) (ğ“ l) := by
  refine tendsto_nhdsWithin_nhds.mpr fun Îµ' hÎµ' â†¦ ?_
  let Îµ := min l Îµ'
  have hâ‚ : 0 < Îµ := by aesop
  have hâ‚‚ : 0 < Îµ / 3 := by positivity
  have hâ‚ƒ : Îµ / 3 < l := lt_of_lt_of_le (div_lt_self hâ‚ (by norm_num)) (min_le_left l Îµ')
  have hâ‚„ : 0 < l - Îµ / 3 := sub_pos.mpr hâ‚ƒ
  have hâ‚… : 0 < l + Îµ / 3 := by positivity
  obtain âŸ¨T, hTâŸ© := exist_finset_lt_sub_mul_tsum_one_div_val_rpow_lt hF hl hâ‚‚ hâ‚ƒ.le
  obtain âŸ¨Î´â‚, _, hÎ´â‚âŸ© := tendsto_nhds_nhds.mp
    (tendsto_sub_mul_sum_one_div_rpow T (val F)) (Îµ / 3) hâ‚‚
  obtain âŸ¨Î´â‚‚, _, hÎ´â‚‚âŸ© := tendsto_nhdsWithin_nhds.mp
    (tendsto_rpow_mul_sub_mul_tsum_one_div_rpow hâ‚„.ne' T) (Îµ / 3) hâ‚‚
  obtain âŸ¨Î´â‚ƒ, _, hÎ´â‚ƒâŸ© := tendsto_nhdsWithin_nhds.mp
    (tendsto_rpow_mul_sub_mul_tsum_one_div_rpow hâ‚….ne' T) (Îµ / 3) hâ‚‚
  let Î´ := min Î´â‚ (min Î´â‚‚ Î´â‚ƒ)
  refine âŸ¨Î´, ?_, ?_âŸ©
  Â· simp_all only [lt_min_iff, and_self, Set.mem_Ioi, Î´]
  Â· intro s hs hsÎ´
    specialize hÎ´â‚ (lt_of_lt_of_le hsÎ´ (by simp [Î´]))
    specialize hÎ´â‚‚ hs (lt_of_lt_of_le hsÎ´ (by simp [Î´]))
    specialize hÎ´â‚ƒ hs (lt_of_lt_of_le hsÎ´ (by simp [Î´]))
    simp_rw [dist_eq_norm, sub_zero, Real.norm_eq_abs, abs_lt] at hÎ´â‚ hÎ´â‚‚ hÎ´â‚ƒ âŠ¢
    constructor
    Â· refine lt_of_le_of_lt (neg_le_neg (min_le_right l Îµ'))
        (lt_trans ?_.trans (sub_lt_sub_right (hT s hs).1 l))
      rw [â† add_thirds (min l Îµ'), neg_add', sub_lt_iff_lt_add, neg_add, sub_add, add_sub_assoc]
      exact add_lt_add hÎ´â‚.1 hÎ´â‚‚.1
    Â· refine lt_of_lt_of_le ((sub_lt_sub_right (hT s hs).2 l).trans ?_) (min_le_right l Îµ')
      rw [â† add_thirds (min l Îµ'), â† sub_lt_iff_lt_add, sub_sub, add_sub_assoc]
      exact add_lt_add hÎ´â‚.2 hÎ´â‚ƒ.2

end tendsto

section main_results

open LSeries.notation

include hl hF

theorem _root_.NumberTheory.LSeriesSummable_card_eq {s : â„‚} (hs : 1 < s.re) :
    LSeriesSummable (â†—(Nat.card {x | F x = Â·})) s := by
  have h : âˆ€á¶  n in atTop, (((Nat.card {x | F x = n} : â„) / (n : â„) ^ (s.re) : â„) : â„‚) =
      term (fun n â†¦ (Nat.card â†‘{x | F x = n})) s.re n := by
    filter_upwards [eventually_gt_atTop 0] with n hn
    rw [term_def, if_neg hn.ne', â† ofReal_natCast n, â† ofReal_cpow (by positivity), ofReal_div,
      ofReal_natCast, div_eq_mul_inv]
  refine (LSeriesSummable_iff_of_re_eq_re (by rfl : s.re = (s.re : â„‚).re)).mpr <|
    Summable.congr_atTop ?_ h
  convert summable_ofReal.mpr ((summable_one_div_val_rpow hF hl hs).comp_injective
    (Equiv.sigmaFiberEquiv (val F)).injective).sigma with n
  exact card_eq_div_pow_eq_tsum hF hl n (by linarith)

theorem _root_.NumberTheory.LSeries.card_eq_residue :
    Tendsto (fun s : â„ â†¦ (s - 1) * L â†—(Nat.card {x | F x = Â·}) s) (ğ“[>] 1) (ğ“ l) := by
  suffices Tendsto (fun s : â„ â†¦ (s - 1) * âˆ‘' n, Nat.card {x | F x = n} / (n : â„) ^ s)
      (ğ“[>] 1) (ğ“ l) by
    rw [â† tendsto_ofReal_iff] at this
    refine this.congr' ?_
    filter_upwards [eventually_mem_nhdsWithin] with s hs
    change 1 < (s : â„‚).re at hs
    simp_rw [LSeries_eq_tsum _ (ne_zero_of_one_lt_re hs), ofReal_mul, ofReal_sub, ofReal_one,
      ofReal_tsum, ofReal_div, ofReal_cpow (Nat.cast_nonneg _), ofReal_natCast]
  exact (tendsto_sub_mul_tsum_one_div_val_rpow hF hl).congr' <| EventuallyEq.rfl.mul
    (tsum_card_eq_div_rpow_eq_tsum hF hl).symm

theorem _root_.NumberTheory.LSeries.abscissaOfAbsConv_card_eq :
    LSeries.abscissaOfAbsConv â†—(Nat.card {x | F x = Â·}) = 1 := by
  refine le_antisymm ?_ ?_
  Â· exact abscissaOfAbsConv_le_of_forall_lt_LSeriesSummable' fun s hs â†¦
      LSeriesSummable_card_eq hF hl (EReal.coe_lt_coe_iff.mp hs)
  Â· have h_lim := LSeries.card_eq_residue hF hl
    contrapose! hl
    suffices Tendsto (fun s : â„ â†¦ (s - 1) * LSeries (fun n â†¦ Nat.card {x | F x = n}) s)
        (ğ“[>] 1) (ğ“ 0) by rw [ofReal_eq_zero.mp (tendsto_nhds_unique h_lim this)]
    rw [show ğ“ 0 = ğ“ (0 * (LSeries (fun n â†¦ â†‘(Nat.card {x | F x = n})) 1)) by rw [zero_mul]]
    refine Tendsto.mul ?_ ?_
    Â· exact tendsto_sub_nhds_zero_iff.mpr continuous_ofReal.continuousWithinAt.tendsto
    Â· suffices ContinuousWithinAt (fun s : â„‚ â†¦ L â†—(Nat.card {x | F x = Â·}) s)
          {s | 1 < s.re} 1 by
        convert Tendsto.comp this.tendsto
          (continuous_ofReal.continuousWithinAt.tendsto_nhdsWithin fun _ x â†¦ x)
      refine ((AnalyticOnNhd.continuousOn (LSeries_analyticOnNhd _)).continuousWithinAt ?_).mono ?_
      Â· simpa using hl
      Â· intro s hs
        exact hl.trans (EReal.coe_lt_coe_iff.mpr hs)

end main_results

end NumberTheory.LSeries.card
