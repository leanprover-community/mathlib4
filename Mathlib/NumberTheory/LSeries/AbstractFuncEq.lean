/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/

import Mathlib.Analysis.MellinTransform

/-!
# Abstract functional equations for Mellin transforms

This file formalises a general version of an argument used to prove functional equations for
zeta and L functions.

### FE-pairs

We define a *weak FE-pair* to be a pair of functions `f, g` on the reals which are locally
integrable on `(0, ‚àû)`, have the form "constant" + "rapidly decaying term" at `‚àû`, and satisfy a
functional equation of the form

`f (1 / x) = Œµ * x ^ k * g x`

for some constants `k ‚àà ‚Ñù` and `Œµ ‚àà ‚ÑÇ`. (Modular forms give rise to natural examples
with `k` being the weight and `Œµ` the global root number; hence the notation.) We could arrange
`Œµ = 1` by scaling `g`; but this is inconvenient in applications so we set things up more generally.

A *strong FE-pair* is a weak FE-pair where the constant terms of `f` and `g` at `‚àû` are both 0.

The main property of these pairs is the following: if `f`, `g` are a weak FE-pair, with constant
terms `f‚ÇÄ` and `g‚ÇÄ` at `‚àû`, then the Mellin transforms `Œõ` and `Œõ'` of `f - f‚ÇÄ` and `g - g‚ÇÄ`
respectively both have meromorphic continuation and satisfy a functional equation of the form

`Œõ (k - s) = Œµ * Œõ' s`.

The poles (and their residues) are explicitly given in terms of `f‚ÇÄ` and `g‚ÇÄ`; in particular, if
`(f, g)` are a strong FE-pair, then the Mellin transforms of `f` and `g` are entire functions.

### Main definitions and results

See the sections *Main theorems on weak FE-pairs* and
*Main theorems on strong FE-pairs* below.

* Strong FE pairs:
  - `StrongFEPair.Œõ` : function of `s : ‚ÑÇ`
  - `StrongFEPair.differentiable_Œõ`: `Œõ` is entire
  - `StrongFEPair.hasMellin`: `Œõ` is everywhere equal to the Mellin transform of `f`
  - `StrongFEPair.functional_equation`: the functional equation for `Œõ`
* Weak FE pairs:
  - `WeakFEPair.Œõ‚ÇÄ`: and `WeakFEPair.Œõ`: functions of `s : ‚ÑÇ`
  - `WeakFEPair.differentiable_Œõ‚ÇÄ`: `Œõ‚ÇÄ` is entire
  - `WeakFEPair.differentiableAt_Œõ`: `Œõ` is differentiable away from `s = 0` and `s = k`
  - `WeakFEPair.hasMellin`: for `k < re s`, `Œõ s` equals the Mellin transform of `f - f‚ÇÄ`
  - `WeakFEPair.functional_equation‚ÇÄ`: the functional equation for `Œõ‚ÇÄ`
  - `WeakFEPair.functional_equation`: the functional equation for `Œõ`
  - `WeakFEPair.Œõ_residue_k`: computation of the residue at `k`
  - `WeakFEPair.Œõ_residue_zero`: computation of the residue at `0`.
-/


/- TODO : Consider extending the results to allow functional equations of the form
`f (N / x) = (const) ‚Ä¢ x ^ k ‚Ä¢ g x` for a real parameter `0 < N`. This could be done either by
generalising the existing proofs in situ, or by a separate wrapper `FEPairWithLevel` which just
applies a scaling factor to `f` and `g` to reduce to the `N = 1` case.
-/

noncomputable section

open Real Complex Filter Topology Asymptotics Set MeasureTheory

variable (E : Type*) [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E]

/-!
## Definitions and symmetry
-/

/-- A structure designed to hold the hypotheses for the Mellin-functional-equation argument
(most general version: rapid decay at `‚àû` up to constant terms) -/
structure WeakFEPair where
  /-- The functions whose Mellin transform we study -/
  (f g : ‚Ñù ‚Üí E)
  /-- Weight (exponent in the functional equation) -/
  (k : ‚Ñù)
  /-- Root number -/
  (Œµ : ‚ÑÇ)
  /-- Constant terms at `‚àû` -/
  (f‚ÇÄ g‚ÇÄ : E)
  (hf_int : LocallyIntegrableOn f (Ioi 0))
  (hg_int : LocallyIntegrableOn g (Ioi 0))
  (hk : 0 < k)
  (hŒµ : Œµ ‚â† 0)
  (h_feq : ‚àÄ x ‚àà Ioi 0, f (1 / x) = (Œµ * ‚Üë(x ^ k)) ‚Ä¢ g x)
  (hf_top (r : ‚Ñù) : (f ¬∑ - f‚ÇÄ) =O[atTop] (¬∑ ^ r))
  (hg_top (r : ‚Ñù) : (g ¬∑ - g‚ÇÄ) =O[atTop] (¬∑ ^ r))

/-- A structure designed to hold the hypotheses for the Mellin-functional-equation argument
(version without constant terms) -/
structure StrongFEPair extends WeakFEPair E where (hf‚ÇÄ : f‚ÇÄ = 0) (hg‚ÇÄ : g‚ÇÄ = 0)

variable {E}

section symmetry

/-- Reformulated functional equation with `f` and `g` interchanged. -/
lemma WeakFEPair.h_feq' (P : WeakFEPair E) (x : ‚Ñù) (hx : 0 < x) :
    P.g (1 / x) = (P.Œµ‚Åª¬π * ‚Üë(x ^ P.k)) ‚Ä¢ P.f x := by
  rw [(div_div_cancel‚ÇÄ (one_ne_zero' ‚Ñù) ‚ñ∏ P.h_feq (1 / x) (one_div_pos.mpr hx):), ‚Üê mul_smul]
  convert (one_smul ‚ÑÇ (P.g (1 / x))).symm using 2
  rw [one_div, inv_rpow hx.le, ofReal_inv]
  field_simp [P.hŒµ, (rpow_pos_of_pos hx _).ne']

/-- The hypotheses are symmetric in `f` and `g`, with the constant `Œµ` replaced by `Œµ‚Åª¬π`. -/
def WeakFEPair.symm (P : WeakFEPair E) : WeakFEPair E where
  f := P.g
  g := P.f
  k := P.k
  Œµ := P.Œµ‚Åª¬π
  f‚ÇÄ := P.g‚ÇÄ
  g‚ÇÄ := P.f‚ÇÄ
  hf_int := P.hg_int
  hg_int := P.hf_int
  hf_top := P.hg_top
  hg_top := P.hf_top
  hŒµ     := inv_ne_zero P.hŒµ
  hk     := P.hk
  h_feq  := P.h_feq'

/-- The hypotheses are symmetric in `f` and `g`, with the constant `Œµ` replaced by `Œµ‚Åª¬π`. -/
def StrongFEPair.symm (P : StrongFEPair E) : StrongFEPair E where
  toWeakFEPair := P.toWeakFEPair.symm
  hf‚ÇÄ := P.hg‚ÇÄ
  hg‚ÇÄ := P.hf‚ÇÄ

end symmetry

namespace WeakFEPair

/-!
## Auxiliary results I: lemmas on asymptotics
-/

/-- As `x ‚Üí 0`, we have `f x = x ^ (-P.k) ‚Ä¢ constant` up to a rapidly decaying error. -/
lemma hf_zero (P : WeakFEPair E) (r : ‚Ñù) :
    (fun x ‚Ü¶ P.f x - (P.Œµ * ‚Üë(x ^ (-P.k))) ‚Ä¢ P.g‚ÇÄ) =O[ùìù[>] 0] (¬∑ ^ r) := by
  have := (P.hg_top (-(r + P.k))).comp_tendsto tendsto_inv_nhdsGT_zero
  simp_rw [IsBigO, IsBigOWith, eventually_nhdsWithin_iff] at this ‚ä¢
  obtain ‚ü®C, hC‚ü© := this
  use ‚ÄñP.Œµ‚Äñ * C
  filter_upwards [hC] with x hC' (hx : 0 < x)
  have h_nv2 : ‚Üë(x ^ P.k) ‚â† (0 : ‚ÑÇ) := ofReal_ne_zero.mpr (rpow_pos_of_pos hx _).ne'
  have h_nv : P.Œµ‚Åª¬π * ‚Üë(x ^ P.k) ‚â† 0 := mul_ne_zero P.symm.hŒµ h_nv2
  specialize hC' hx
  simp_rw [Function.comp_apply, ‚Üê one_div, P.h_feq' _ hx] at hC'
  rw [‚Üê ((mul_inv_cancel‚ÇÄ h_nv).symm ‚ñ∏ one_smul ‚ÑÇ P.g‚ÇÄ :), mul_smul _ _ P.g‚ÇÄ, ‚Üê smul_sub, norm_smul,
    ‚Üê le_div_iff‚ÇÄ' (lt_of_le_of_ne (norm_nonneg _) (norm_ne_zero_iff.mpr h_nv).symm)] at hC'
  convert hC' using 1
  ¬∑ congr 3
    rw [rpow_neg hx.le]
    field_simp
  ¬∑ simp_rw [norm_mul, norm_real, one_div, inv_rpow hx.le, rpow_neg hx.le, inv_inv, norm_inv,
      norm_of_nonneg (rpow_pos_of_pos hx _).le, rpow_add hx]
    field_simp
    ring

/-- Power asymptotic for `f - f‚ÇÄ` as `x ‚Üí 0`. -/
lemma hf_zero' (P : WeakFEPair E) :
    (fun x : ‚Ñù ‚Ü¶ P.f x - P.f‚ÇÄ) =O[ùìù[>] 0] (¬∑ ^ (-P.k)) := by
  simp_rw [‚Üê fun x ‚Ü¶ sub_add_sub_cancel (P.f x) ((P.Œµ * ‚Üë(x ^ (-P.k))) ‚Ä¢ P.g‚ÇÄ) P.f‚ÇÄ]
  refine (P.hf_zero _).add (IsBigO.sub ?_ ?_)
  ¬∑ rw [‚Üê isBigO_norm_norm]
    simp_rw [mul_smul, norm_smul, mul_comm _ ‚ÄñP.g‚ÇÄ‚Äñ, ‚Üê mul_assoc, norm_real]
    apply (isBigO_refl _ _).const_mul_left
  ¬∑ refine IsBigO.of_bound ‚ÄñP.f‚ÇÄ‚Äñ (eventually_nhdsWithin_iff.mpr ?_)
    filter_upwards [eventually_le_nhds zero_lt_one] with x hx' (hx : 0 < x)
    apply le_mul_of_one_le_right (norm_nonneg _)
    rw [norm_of_nonneg (rpow_pos_of_pos hx _).le, rpow_neg hx.le]
    exact (one_le_inv‚ÇÄ (rpow_pos_of_pos hx _)).2 (rpow_le_one hx.le hx' P.hk.le)

end WeakFEPair

namespace StrongFEPair

variable (P : StrongFEPair E)

/-- As `x ‚Üí ‚àû`, `f x` decays faster than any power of `x`. -/
lemma hf_top' (r : ‚Ñù) : P.f =O[atTop] (¬∑ ^ r) := by
  simpa [P.hf‚ÇÄ] using P.hf_top r

/-- As `x ‚Üí 0`, `f x` decays faster than any power of `x`. -/
lemma hf_zero' (r : ‚Ñù) : P.f =O[ùìù[>] 0] (¬∑ ^ r) := by
  simpa using (P.hg‚ÇÄ ‚ñ∏ P.hf_zero r :)

/-!
## Main theorems on strong FE-pairs
-/

/-- The completed L-function. -/
def Œõ : ‚ÑÇ ‚Üí E := mellin P.f

/-- The Mellin transform of `f` is well-defined and equal to `P.Œõ s`, for all `s`. -/
theorem hasMellin (s : ‚ÑÇ) : HasMellin P.f s (P.Œõ s) :=
  let ‚ü®_, ht‚ü© := exists_gt s.re
  let ‚ü®_, hu‚ü© := exists_lt s.re
  ‚ü®mellinConvergent_of_isBigO_rpow P.hf_int (P.hf_top' _) ht (P.hf_zero' _) hu, rfl‚ü©

lemma Œõ_eq : P.Œõ = mellin P.f := rfl

lemma symm_Œõ_eq : P.symm.Œõ = mellin P.g := rfl

/-- If `(f, g)` are a strong FE pair, then the Mellin transform of `f` is entire. -/
theorem differentiable_Œõ : Differentiable ‚ÑÇ P.Œõ := fun s ‚Ü¶
  let ‚ü®_, ht‚ü© := exists_gt s.re
  let ‚ü®_, hu‚ü© := exists_lt s.re
  mellin_differentiableAt_of_isBigO_rpow P.hf_int (P.hf_top' _) ht (P.hf_zero' _) hu

/-- Main theorem about strong FE pairs: if `(f, g)` are a strong FE pair, then the Mellin
transforms of `f` and `g` are related by `s ‚Ü¶ k - s`.

This is proved by making a substitution `t ‚Ü¶ t‚Åª¬π` in the Mellin transform integral. -/
theorem functional_equation (s : ‚ÑÇ) :
    P.Œõ (P.k - s) = P.Œµ ‚Ä¢ P.symm.Œõ s := by
  -- unfold definition:
  rw [P.Œõ_eq, P.symm_Œõ_eq]
  -- substitute `t ‚Ü¶ t‚Åª¬π` in `mellin P.g s`
  have step1 := mellin_comp_rpow P.g (-s) (-1)
  simp_rw [abs_neg, abs_one, inv_one, one_smul, ofReal_neg, ofReal_one, div_neg, div_one, neg_neg,
    rpow_neg_one, ‚Üê one_div] at step1
  -- introduce a power of `t` to match the hypothesis `P.h_feq`
  have step2 := mellin_cpow_smul (fun t ‚Ü¶ P.g (1 / t)) (P.k - s) (-P.k)
  rw [‚Üê sub_eq_add_neg, sub_right_comm, sub_self, zero_sub, step1] at step2
  -- put in the constant `P.Œµ`
  have step3 := mellin_const_smul (fun t ‚Ü¶ (t : ‚ÑÇ) ^ (-P.k : ‚ÑÇ) ‚Ä¢ P.g (1 / t)) (P.k - s) P.Œµ
  rw [step2] at step3
  rw [‚Üê step3]
  -- now the integrand matches `P.h_feq'` on `Ioi 0`, so we can apply `setIntegral_congr_fun`
  refine setIntegral_congr_fun measurableSet_Ioi (fun t ht ‚Ü¶ ?_)
  simp_rw [P.h_feq' t ht, ‚Üê mul_smul]
  -- some simple `cpow` arithmetic to finish
  rw [cpow_neg, ofReal_cpow (le_of_lt ht)]
  have : (t : ‚ÑÇ) ^ (P.k : ‚ÑÇ) ‚â† 0 := by simpa [‚Üê ofReal_cpow ht.le] using (rpow_pos_of_pos ht _).ne'
  field_simp [P.hŒµ]

end StrongFEPair

namespace WeakFEPair

variable (P : WeakFEPair E)

/-!
## Auxiliary results II: building a strong FE-pair from a weak FE-pair
-/

/-- Piecewise modified version of `f` with optimal asymptotics. We deliberately choose intervals
which don't quite join up, so the function is `0` at `x = 1`, in order to maintain symmetry;
there is no "good" choice of value at `1`. -/
def f_modif : ‚Ñù ‚Üí E :=
  (Ioi 1).indicator (fun x ‚Ü¶ P.f x - P.f‚ÇÄ) +
  (Ioo 0 1).indicator (fun x ‚Ü¶ P.f x - (P.Œµ * ‚Üë(x ^ (-P.k))) ‚Ä¢ P.g‚ÇÄ)

/-- Piecewise modified version of `g` with optimal asymptotics. -/
def g_modif : ‚Ñù ‚Üí E :=
  (Ioi 1).indicator (fun x ‚Ü¶ P.g x - P.g‚ÇÄ) +
  (Ioo 0 1).indicator (fun x ‚Ü¶ P.g x - (P.Œµ‚Åª¬π * ‚Üë(x ^ (-P.k))) ‚Ä¢ P.f‚ÇÄ)

lemma hf_modif_int :
    LocallyIntegrableOn P.f_modif (Ioi 0) := by
  have : LocallyIntegrableOn (fun x : ‚Ñù ‚Ü¶ (P.Œµ * ‚Üë(x ^ (-P.k))) ‚Ä¢ P.g‚ÇÄ) (Ioi 0) := by
    refine ContinuousOn.locallyIntegrableOn ?_ measurableSet_Ioi
    refine continuousOn_of_forall_continuousAt (fun x (hx : 0 < x) ‚Ü¶ ?_)
    refine (continuousAt_const.mul ?_).smul continuousAt_const
    exact continuous_ofReal.continuousAt.comp (continuousAt_rpow_const _ _ (Or.inl hx.ne'))
  refine LocallyIntegrableOn.add (fun x hx ‚Ü¶ ?_) (fun x hx ‚Ü¶ ?_)
  ¬∑ obtain ‚ü®s, hs, hs'‚ü© := P.hf_int.sub (locallyIntegrableOn_const _) x hx
    refine ‚ü®s, hs, ?_‚ü©
    rw [IntegrableOn, integrable_indicator_iff measurableSet_Ioi, IntegrableOn,
      Measure.restrict_restrict measurableSet_Ioi, ‚Üê IntegrableOn]
    exact hs'.mono_set Set.inter_subset_right
  ¬∑ obtain ‚ü®s, hs, hs'‚ü© := P.hf_int.sub this x hx
    refine ‚ü®s, hs, ?_‚ü©
    rw [IntegrableOn, integrable_indicator_iff measurableSet_Ioo, IntegrableOn,
      Measure.restrict_restrict measurableSet_Ioo, ‚Üê IntegrableOn]
    exact hs'.mono_set Set.inter_subset_right

lemma hf_modif_FE (x : ‚Ñù) (hx : 0 < x) :
    P.f_modif (1 / x) = (P.Œµ * ‚Üë(x ^ P.k)) ‚Ä¢ P.g_modif x := by
  rcases lt_trichotomy 1 x with hx' | rfl | hx'
  ¬∑ have : 1 / x < 1 := by rwa [one_div_lt hx one_pos, div_one]
    rw [f_modif, Pi.add_apply, indicator_of_notMem (notMem_Ioi.mpr this.le),
      zero_add, indicator_of_mem (mem_Ioo.mpr ‚ü®div_pos one_pos hx, this‚ü©), g_modif, Pi.add_apply,
      indicator_of_mem (mem_Ioi.mpr hx'), indicator_of_notMem
      (notMem_Ioo_of_ge hx'.le), add_zero, P.h_feq _ hx, smul_sub]
    simp_rw [rpow_neg (one_div_pos.mpr hx).le, one_div, inv_rpow hx.le, inv_inv]
  ¬∑ simp [f_modif, g_modif]
  ¬∑ have : 1 < 1 / x := by rwa [lt_one_div one_pos hx, div_one]
    rw [f_modif, Pi.add_apply, indicator_of_mem (mem_Ioi.mpr this),
      indicator_of_notMem (notMem_Ioo_of_ge this.le), add_zero, g_modif, Pi.add_apply,
      indicator_of_notMem (notMem_Ioi.mpr hx'.le),
      indicator_of_mem (mem_Ioo.mpr ‚ü®hx, hx'‚ü©), zero_add, P.h_feq _ hx, smul_sub]
    simp_rw [rpow_neg hx.le, ‚Üê mul_smul]
    field_simp [(rpow_pos_of_pos hx P.k).ne', P.hŒµ]

/-- Given a weak FE-pair `(f, g)`, modify it into a strong FE-pair by subtracting suitable
correction terms from `f` and `g`. -/
def toStrongFEPair : StrongFEPair E where
  f := P.f_modif
  g := P.symm.f_modif
  k := P.k
  Œµ := P.Œµ
  f‚ÇÄ := 0
  g‚ÇÄ := 0
  hf_int   := P.hf_modif_int
  hg_int   := P.symm.hf_modif_int
  h_feq    := P.hf_modif_FE
  hŒµ       := P.hŒµ
  hk       := P.hk
  hf‚ÇÄ      := rfl
  hg‚ÇÄ      := rfl
  hf_top r := by
    refine (P.hf_top r).congr' ?_ (by rfl)
    filter_upwards [eventually_gt_atTop 1] with x hx
    rw [f_modif, Pi.add_apply, indicator_of_mem (mem_Ioi.mpr hx),
      indicator_of_notMem (notMem_Ioo_of_ge hx.le), add_zero, sub_zero]
  hg_top r := by
    refine (P.hg_top r).congr' ?_ (by rfl)
    filter_upwards [eventually_gt_atTop 1] with x hx
    rw [f_modif, Pi.add_apply, indicator_of_mem (mem_Ioi.mpr hx),
      indicator_of_notMem (notMem_Ioo_of_ge hx.le), add_zero, sub_zero]
    rfl

/- Alternative form for the difference between `f - f‚ÇÄ` and its modified term. -/
lemma f_modif_aux1 : EqOn (fun x ‚Ü¶ P.f_modif x - P.f x + P.f‚ÇÄ)
    ((Ioo 0 1).indicator (fun x : ‚Ñù ‚Ü¶ P.f‚ÇÄ - (P.Œµ * ‚Üë(x ^ (-P.k))) ‚Ä¢ P.g‚ÇÄ)
    + ({1} : Set ‚Ñù).indicator (fun _ ‚Ü¶ P.f‚ÇÄ - P.f 1)) (Ioi 0) := by
  intro x (hx : 0 < x)
  simp_rw [f_modif, Pi.add_apply]
  rcases lt_trichotomy x 1 with hx' | rfl | hx'
  ¬∑ simp_rw [indicator_of_notMem (notMem_Ioi.mpr hx'.le),
      indicator_of_mem (mem_Ioo.mpr ‚ü®hx, hx'‚ü©),
      indicator_of_notMem (mem_singleton_iff.not.mpr hx'.ne)]
    abel
  ¬∑ simp [add_comm, sub_eq_add_neg]
  ¬∑ simp_rw [indicator_of_mem (mem_Ioi.mpr hx'),
      indicator_of_notMem (notMem_Ioo_of_ge hx'.le),
      indicator_of_notMem (mem_singleton_iff.not.mpr hx'.ne')]
    abel

/-- Compute the Mellin transform of the modifying term used to kill off the constants at
`0` and `‚àû`. -/
lemma f_modif_aux2 [CompleteSpace E] {s : ‚ÑÇ} (hs : P.k < re s) :
    mellin (fun x ‚Ü¶ P.f_modif x - P.f x + P.f‚ÇÄ) s = (1 / s) ‚Ä¢ P.f‚ÇÄ + (P.Œµ  / (P.k - s)) ‚Ä¢ P.g‚ÇÄ := by
  have h_re1 : -1 < re (s - 1) := by simpa using P.hk.trans hs
  have h_re2 : -1 < re (s - P.k - 1) := by simpa using hs
  calc
  _ = ‚à´ (x : ‚Ñù) in Ioi 0, (x : ‚ÑÇ) ^ (s - 1) ‚Ä¢
      ((Ioo 0 1).indicator (fun t : ‚Ñù ‚Ü¶ P.f‚ÇÄ - (P.Œµ * ‚Üë(t ^ (-P.k))) ‚Ä¢ P.g‚ÇÄ) x
      + ({1} : Set ‚Ñù).indicator (fun _ ‚Ü¶ P.f‚ÇÄ - P.f 1) x) :=
    setIntegral_congr_fun measurableSet_Ioi (fun x hx ‚Ü¶ by simp [f_modif_aux1 P hx])
  _ = ‚à´ (x : ‚Ñù) in Ioi 0, (x : ‚ÑÇ) ^ (s - 1) ‚Ä¢ ((Ioo 0 1).indicator
      (fun t : ‚Ñù ‚Ü¶ P.f‚ÇÄ - (P.Œµ * ‚Üë(t ^ (-P.k))) ‚Ä¢ P.g‚ÇÄ) x) := by
    refine setIntegral_congr_ae measurableSet_Ioi (eventually_of_mem (U := {1}·∂ú)
        (compl_mem_ae_iff.mpr (subsingleton_singleton.measure_zero _)) (fun x hx _ ‚Ü¶ ?_))
    rw [indicator_of_notMem hx, add_zero]
  _ = ‚à´ (x : ‚Ñù) in Ioc 0 1, (x : ‚ÑÇ) ^ (s - 1) ‚Ä¢ (P.f‚ÇÄ - (P.Œµ * ‚Üë(x ^ (-P.k))) ‚Ä¢ P.g‚ÇÄ) := by
    simp_rw [‚Üê indicator_smul, setIntegral_indicator measurableSet_Ioo,
      inter_eq_right.mpr Ioo_subset_Ioi_self, integral_Ioc_eq_integral_Ioo]
  _ = ‚à´ x : ‚Ñù in Ioc 0 1, ((x : ‚ÑÇ) ^ (s - 1) ‚Ä¢ P.f‚ÇÄ - P.Œµ ‚Ä¢ (x : ‚ÑÇ) ^ (s - P.k - 1) ‚Ä¢ P.g‚ÇÄ) := by
    refine setIntegral_congr_fun measurableSet_Ioc (fun x ‚ü®hx, _‚ü© ‚Ü¶ ?_)
    rw [ofReal_cpow hx.le, ofReal_neg, smul_sub, ‚Üê mul_smul, mul_comm, mul_assoc, mul_smul,
      mul_comm, ‚Üê cpow_add _ _ (ofReal_ne_zero.mpr hx.ne'), ‚Üê sub_eq_add_neg, sub_right_comm]
  _ = (‚à´ (x : ‚Ñù) in Ioc 0 1, (x : ‚ÑÇ) ^ (s - 1)) ‚Ä¢ P.f‚ÇÄ
        - P.Œµ ‚Ä¢ (‚à´ (x : ‚Ñù) in Ioc 0 1, (x : ‚ÑÇ) ^ (s - P.k - 1)) ‚Ä¢ P.g‚ÇÄ := by
    rw [integral_sub, integral_smul, integral_smul_const, integral_smul_const]
    ¬∑ apply Integrable.smul_const
      rw [‚Üê IntegrableOn, ‚Üê intervalIntegrable_iff_integrableOn_Ioc_of_le zero_le_one]
      exact intervalIntegral.intervalIntegrable_cpow' h_re1
    ¬∑ refine (Integrable.smul_const ?_ _).smul _
      rw [‚Üê IntegrableOn, ‚Üê intervalIntegrable_iff_integrableOn_Ioc_of_le zero_le_one]
      exact intervalIntegral.intervalIntegrable_cpow' h_re2
  _ = _ := by simp_rw [‚Üê intervalIntegral.integral_of_le zero_le_one,
      integral_cpow (Or.inl h_re1), integral_cpow (Or.inl h_re2), ofReal_zero, ofReal_one,
      one_cpow, sub_add_cancel, zero_cpow fun h ‚Ü¶ lt_irrefl _ (P.hk.le.trans_lt (zero_re ‚ñ∏ h ‚ñ∏ hs)),
      zero_cpow (sub_ne_zero.mpr (fun h ‚Ü¶ lt_irrefl _ ((ofReal_re _) ‚ñ∏ h ‚ñ∏ hs)) : s - P.k ‚â† 0),
      sub_zero, sub_eq_add_neg (_ ‚Ä¢  _), ‚Üê mul_smul, ‚Üê neg_smul, mul_one_div, ‚Üê div_neg, neg_sub]

/-!
## Main theorems on weak FE-pairs
-/

/-- An entire function which differs from the Mellin transform of `f - f‚ÇÄ`, where defined, by a
correction term of the form `A / s + B / (k - s)`. -/
def Œõ‚ÇÄ : ‚ÑÇ ‚Üí E := mellin P.f_modif

/-- A meromorphic function which agrees with the Mellin transform of `f - f‚ÇÄ` where defined -/
def Œõ (s : ‚ÑÇ) : E := P.Œõ‚ÇÄ s - (1 / s) ‚Ä¢ P.f‚ÇÄ - (P.Œµ / (P.k - s)) ‚Ä¢ P.g‚ÇÄ

lemma Œõ‚ÇÄ_eq (s : ‚ÑÇ) : P.Œõ‚ÇÄ s = P.Œõ s + (1 / s) ‚Ä¢ P.f‚ÇÄ + (P.Œµ / (P.k - s)) ‚Ä¢ P.g‚ÇÄ := by
  unfold Œõ Œõ‚ÇÄ
  abel

lemma symm_Œõ‚ÇÄ_eq (s : ‚ÑÇ) :
    P.symm.Œõ‚ÇÄ s = P.symm.Œõ s + (1 / s) ‚Ä¢ P.g‚ÇÄ + (P.Œµ‚Åª¬π / (P.k - s)) ‚Ä¢ P.f‚ÇÄ := by
  rw [P.symm.Œõ‚ÇÄ_eq]
  rfl

theorem differentiable_Œõ‚ÇÄ : Differentiable ‚ÑÇ P.Œõ‚ÇÄ := P.toStrongFEPair.differentiable_Œõ

theorem differentiableAt_Œõ {s : ‚ÑÇ} (hs : s ‚â† 0 ‚à® P.f‚ÇÄ = 0) (hs' : s ‚â† P.k ‚à® P.g‚ÇÄ = 0) :
    DifferentiableAt ‚ÑÇ P.Œõ s := by
  refine ((P.differentiable_Œõ‚ÇÄ s).sub ?_).sub ?_
  ¬∑ rcases hs with hs | hs
    ¬∑ simpa using (differentiableAt_inv hs).smul_const _
    ¬∑ simp [hs]
  ¬∑ rcases hs' with hs' | hs'
    ¬∑ apply DifferentiableAt.smul_const
      apply (differentiableAt_const _).div ((differentiableAt_const _).sub (differentiable_id _))
      simpa [sub_eq_zero, eq_comm]
    ¬∑ simp [hs']

/-- Relation between `Œõ s` and the Mellin transform of `f - f‚ÇÄ`, where the latter is defined. -/
theorem hasMellin [CompleteSpace E]
    {s : ‚ÑÇ} (hs : P.k < s.re) : HasMellin (P.f ¬∑ - P.f‚ÇÄ) s (P.Œõ s) := by
  have hc1 : MellinConvergent (P.f ¬∑ - P.f‚ÇÄ) s :=
    let ‚ü®_, ht‚ü© := exists_gt s.re
    mellinConvergent_of_isBigO_rpow (P.hf_int.sub (locallyIntegrableOn_const _)) (P.hf_top _) ht
      P.hf_zero' hs
  refine ‚ü®hc1, ?_‚ü©
  have hc2 : HasMellin P.f_modif s (P.Œõ‚ÇÄ s) := P.toStrongFEPair.hasMellin s
  have hc3 : mellin (fun x ‚Ü¶ f_modif P x - f P x + P.f‚ÇÄ) s =
    (1 / s) ‚Ä¢ P.f‚ÇÄ + (P.Œµ / (‚ÜëP.k - s)) ‚Ä¢ P.g‚ÇÄ := P.f_modif_aux2 hs
  have := (hasMellin_sub hc2.1 hc1).2
  simp_rw [‚Üê sub_add, hc3, eq_sub_iff_add_eq, ‚Üê eq_sub_iff_add_eq', ‚Üê sub_sub] at this
  exact this

/-- Functional equation formulated for `Œõ‚ÇÄ`. -/
theorem functional_equation‚ÇÄ (s : ‚ÑÇ) : P.Œõ‚ÇÄ (P.k - s) = P.Œµ ‚Ä¢ P.symm.Œõ‚ÇÄ s :=
  P.toStrongFEPair.functional_equation s

/-- Functional equation formulated for `Œõ`. -/
theorem functional_equation (s : ‚ÑÇ) :
    P.Œõ (P.k - s) = P.Œµ ‚Ä¢ P.symm.Œõ s := by
  linear_combination (norm := module) P.functional_equation‚ÇÄ s - P.Œõ‚ÇÄ_eq (P.k - s)
    + congr(P.Œµ ‚Ä¢ $(P.symm_Œõ‚ÇÄ_eq s)) + congr(($(mul_inv_cancel‚ÇÄ P.hŒµ) / ((P.k:‚ÑÇ) - s)) ‚Ä¢ P.f‚ÇÄ)

/-- The residue of `Œõ` at `s = k` is equal to `Œµ ‚Ä¢ g‚ÇÄ`. -/
theorem Œõ_residue_k :
    Tendsto (fun s : ‚ÑÇ ‚Ü¶ (s - P.k) ‚Ä¢ P.Œõ s) (ùìù[‚â†] P.k) (ùìù (P.Œµ ‚Ä¢ P.g‚ÇÄ)) := by
  simp_rw [Œõ, smul_sub, (by simp : ùìù (P.Œµ ‚Ä¢ P.g‚ÇÄ) = ùìù (0 - 0 - -P.Œµ ‚Ä¢ P.g‚ÇÄ))]
  refine ((Tendsto.sub ?_ ?_).mono_left nhdsWithin_le_nhds).sub ?_
  ¬∑ rw [(by rw [sub_self, zero_smul] : ùìù 0 = ùìù ((P.k - P.k : ‚ÑÇ) ‚Ä¢ P.Œõ‚ÇÄ P.k))]
    apply ((continuous_sub_right _).smul P.differentiable_Œõ‚ÇÄ.continuous).tendsto
  ¬∑ rw [(by rw [sub_self, zero_smul] : ùìù 0 = ùìù ((P.k - P.k : ‚ÑÇ) ‚Ä¢ (1 / P.k : ‚ÑÇ) ‚Ä¢ P.f‚ÇÄ))]
    refine (continuous_sub_right _).continuousAt.smul (ContinuousAt.smul ?_ continuousAt_const)
    exact continuousAt_const.div continuousAt_id (ofReal_ne_zero.mpr P.hk.ne')
  ¬∑ refine (tendsto_const_nhds.mono_left nhdsWithin_le_nhds).congr' ?_
    refine eventually_nhdsWithin_of_forall (fun s (hs : s ‚â† P.k) ‚Ü¶ ?_)
    match_scalars
    field_simp [sub_ne_zero.mpr hs.symm]
    ring

/-- The residue of `Œõ` at `s = 0` is equal to `-f‚ÇÄ`. -/
theorem Œõ_residue_zero :
    Tendsto (fun s : ‚ÑÇ ‚Ü¶ s ‚Ä¢ P.Œõ s) (ùìù[‚â†] 0) (ùìù (-P.f‚ÇÄ)) := by
  simp_rw [Œõ, smul_sub, (by simp : ùìù (-P.f‚ÇÄ) = ùìù (((0 : ‚ÑÇ) ‚Ä¢ P.Œõ‚ÇÄ 0) - P.f‚ÇÄ - 0))]
  refine ((Tendsto.mono_left ?_ nhdsWithin_le_nhds).sub ?_).sub ?_
  ¬∑ exact (continuous_id.smul P.differentiable_Œõ‚ÇÄ.continuous).tendsto _
  ¬∑ refine (tendsto_const_nhds.mono_left nhdsWithin_le_nhds).congr' ?_
    refine eventually_nhdsWithin_of_forall (fun s (hs : s ‚â† 0) ‚Ü¶ ?_)
    match_scalars
    field_simp [sub_ne_zero.mpr hs.symm]
  ¬∑ rw [show ùìù 0 = ùìù ((0 : ‚ÑÇ) ‚Ä¢ (P.Œµ / (P.k - 0 : ‚ÑÇ)) ‚Ä¢ P.g‚ÇÄ) by rw [zero_smul]]
    exact (continuousAt_id.smul ((continuousAt_const.div ((continuous_sub_left _).continuousAt)
      (by simpa using P.hk.ne')).smul continuousAt_const)).mono_left nhdsWithin_le_nhds

end WeakFEPair
