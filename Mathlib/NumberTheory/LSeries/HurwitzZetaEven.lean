/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.NumberTheory.LSeries.AbstractFuncEq
import Mathlib.NumberTheory.ModularForms.JacobiTheta.Bounds
import Mathlib.Analysis.SpecialFunctions.Gamma.Deligne
import Mathlib.NumberTheory.LSeries.MellinEqDirichlet
import Mathlib.NumberTheory.LSeries.Basic
import Mathlib.Analysis.Complex.RemovableSingularity

/-!
# Even Hurwitz zeta functions

In this file we study the functions on `â„‚` which are the meromorphic continuation of the following
series (convergent for `1 < re s`), where `a âˆˆ â„` is a parameter:

`hurwitzZetaEven a s = 1 / 2 * âˆ‘' n : â„¤, 1 / |n + a| ^ s`

and

`cosZeta a s = âˆ‘' n : â„•, cos (2 * Ï€ * a * n) / |n| ^ s`.

Note that the term for `n = -a` in the first sum is omitted if `a` is an integer, and the term for
`n = 0` is omitted in the second sum (always).

Of course, we cannot *define* these functions by the above formulae (since existence of the
meromorphic continuation is not at all obvious); we in fact construct them as Mellin transforms of
various versions of the Jacobi theta function.

We also define completed versions of these functions with nicer functional equations (satisfying
`completedHurwitzZetaEven a s = Gammaâ„ s * hurwitzZetaEven a s`, and similarly for `cosZeta`); and
modified versions with a subscript `0`, which are entire functions differing from the above by
multiples of `1 / s` and `1 / (1 - s)`.

##Â Main definitions and theorems
* `hurwitzZetaEven` and `cosZeta`: the zeta functions
* `completedHurwitzZetaEven` and `completedCosZeta`: completed variants
* `differentiableAt_hurwitzZetaEven` and `differentiableAt_cosZeta`:
  differentiability away from `s = 1`
* `completedHurwitzZetaEven_one_sub`: the functional equation
  `completedHurwitzZetaEven a (1 - s) = completedCosZeta a s`
* `hasSum_int_hurwitzZetaEven` and `hasSum_nat_cosZeta`: relation between the zeta functions and
  the corresponding Dirichlet series for `1 < re s`.
-/
noncomputable section

open Complex Filter Topology Asymptotics Real Set Classical MeasureTheory

section kernel_defs
/-!
## Definitions and elementary properties of kernels
-/

/-- Even Hurwitz zeta kernel (function whose Mellin transform will be the even part of the
completed Hurwit zeta function). See `evenKernel_def` for the defining formula, and
`hasSum_int_evenKernel` for an expression as a sum over `â„¤`. -/
@[irreducible] def evenKernel (a : UnitAddCircle) (x : â„) : â„ :=
  (show Function.Periodic
    (fun Î¾ : â„ â†¦ rexp (-Ï€ * Î¾ ^ 2 * x) * re (jacobiThetaâ‚‚ (Î¾ * I * x) (I * x))) 1 by
      intro Î¾
      simp only [ofReal_add, ofReal_one, add_mul, one_mul, jacobiThetaâ‚‚_add_left']
      have : cexp (-â†‘Ï€ * I * ((I * â†‘x) + 2 * (â†‘Î¾ * I * â†‘x))) = rexp (Ï€ * (x + 2 * Î¾ * x)) := by
        ring_nf
        simp only [I_sq, mul_neg, mul_one, neg_mul, neg_neg, sub_neg_eq_add, ofReal_exp, ofReal_add,
          ofReal_mul, ofReal_ofNat]
      rw [this, re_ofReal_mul, â† mul_assoc, â† Real.exp_add]
      congr
      ring).lift a

lemma evenKernel_def (a x : â„) :
    â†‘(evenKernel â†‘a x) = cexp (-Ï€ * a ^ 2 * x) * jacobiThetaâ‚‚ (a * I * x) (I * x) := by
  unfold evenKernel
  simp only [neg_mul, Function.Periodic.lift_coe, ofReal_mul, ofReal_exp, ofReal_neg, ofReal_pow,
    re_eq_add_conj, jacobiThetaâ‚‚_conj, map_mul, conj_ofReal, conj_I, mul_neg, neg_neg,
    jacobiThetaâ‚‚_neg_left, â† mul_two, mul_div_cancel_rightâ‚€ _ (two_ne_zero' â„‚)]

/-- For `x â‰¤ 0` the defining sum diverges, so the kernel is 0. -/
lemma evenKernel_undef (a : UnitAddCircle) {x : â„} (hx : x â‰¤ 0) : evenKernel a x = 0 := by
  have : (I * â†‘x).im â‰¤ 0 := by rwa [I_mul_im, ofReal_re]
  induction' a using QuotientAddGroup.induction_on' with a'
  rw [â† ofReal_inj, evenKernel_def, jacobiThetaâ‚‚_undef _ this, mul_zero, ofReal_zero]

/-- Cosine Hurwitz zeta kernel. See `cosKernel_def` for the defining formula, and
`hasSum_int_cosKernel` for expression as a sum. -/
@[irreducible] def cosKernel (a : UnitAddCircle) (x : â„) : â„ :=
  (show Function.Periodic (fun Î¾ : â„ â†¦ re (jacobiThetaâ‚‚ Î¾ (I * x))) 1 by
    intro Î¾; simp_rw [ofReal_add, ofReal_one, jacobiThetaâ‚‚_add_left]).lift a

lemma cosKernel_def (a x : â„) : â†‘(cosKernel â†‘a x) = jacobiThetaâ‚‚ a (I * x) := by
  unfold cosKernel
  simp only [Function.Periodic.lift_coe, re_eq_add_conj, jacobiThetaâ‚‚_conj, conj_ofReal, map_mul,
    conj_I, neg_mul, neg_neg, â† mul_two, mul_div_cancel_rightâ‚€ _ (two_ne_zero' â„‚)]

lemma cosKernel_undef (a : UnitAddCircle) {x : â„} (hx : x â‰¤ 0) : cosKernel a x = 0 := by
  induction' a using QuotientAddGroup.induction_on' with a'
  rw [â† ofReal_inj, cosKernel_def, jacobiThetaâ‚‚_undef, ofReal_zero]
  rwa [I_mul_im, ofReal_re]

/-- For `a = 0`, both kernels agree. -/
lemma evenKernel_eq_cosKernel_of_zero : evenKernel 0 = cosKernel 0 := by
  ext1 x
  simp only [â† QuotientAddGroup.mk_zero, â† ofReal_inj, evenKernel_def, ofReal_zero, sq, mul_zero,
    zero_mul, Complex.exp_zero, one_mul, cosKernel_def]

lemma evenKernel_neg (a : UnitAddCircle) (x : â„) : evenKernel (-a) x = evenKernel a x := by
  induction' a using QuotientAddGroup.induction_on' with a'
  simp only [â† QuotientAddGroup.mk_neg, â† ofReal_inj, evenKernel_def, ofReal_neg, neg_sq, neg_mul,
    jacobiThetaâ‚‚_neg_left]

lemma cosKernel_neg (a : UnitAddCircle) (x : â„) : cosKernel (-a) x = cosKernel a x := by
  induction' a using QuotientAddGroup.induction_on' with a'
  simp only [â† QuotientAddGroup.mk_neg, â† ofReal_inj, cosKernel_def, ofReal_neg,
    jacobiThetaâ‚‚_neg_left]

lemma continuousOn_evenKernel (a : UnitAddCircle) : ContinuousOn (evenKernel a) (Ioi 0) := by
  induction' a using QuotientAddGroup.induction_on' with a'
  apply continuous_re.comp_continuousOn (f := fun x â†¦ (evenKernel a' x : â„‚))
  simp only [evenKernel_def a']
  refine ContinuousAt.continuousOn (fun x hx â†¦ ((Continuous.continuousAt ?_).mul ?_))
  Â· exact Complex.continuous_exp.comp (continuous_const.mul continuous_ofReal)
  Â· have h := continuousAt_jacobiThetaâ‚‚ (a' * I * x) (?_ : 0 < im (I * x))
    Â· exact h.comp (f := fun u : â„ â†¦ (a' * I * u, I * u)) (by fun_prop)
    Â· rwa [mul_im, I_re, I_im, zero_mul, one_mul, zero_add, ofReal_re]

lemma continuousOn_cosKernel (a : UnitAddCircle) : ContinuousOn (cosKernel a) (Ioi 0) := by
  induction' a using QuotientAddGroup.induction_on' with a'
  apply continuous_re.comp_continuousOn (f := fun x â†¦ (cosKernel a' x : â„‚))
  simp only [cosKernel_def]
  refine ContinuousAt.continuousOn (fun x hx â†¦ ?_)
  have : 0 < im (I * x) := by rwa [mul_im, I_re, I_im, zero_mul, one_mul, zero_add, ofReal_re]
  exact (continuousAt_jacobiThetaâ‚‚ a' this).comp (f := fun u : â„ â†¦ (_, I * u)) (by fun_prop)

lemma evenKernel_functional_equation (a : UnitAddCircle) (x : â„) :
    evenKernel a x = 1 / x ^ (1 / 2 : â„) * cosKernel a (1 / x) := by
  rcases le_or_lt x 0 with hx | hx
  Â· rw [evenKernel_undef _ hx, cosKernel_undef, mul_zero]
    exact div_nonpos_of_nonneg_of_nonpos zero_le_one hx
  induction' a using QuotientAddGroup.induction_on' with a
  rw [â† ofReal_inj, ofReal_mul, evenKernel_def, cosKernel_def, jacobiThetaâ‚‚_functional_equation]
  have h1 : I * â†‘(1 / x) = -1 / (I * x) := by
    push_cast
    rw [â† div_div, mul_one_div, div_I, neg_one_mul, neg_neg]
  have hx' : I * x â‰  0 := mul_ne_zero I_ne_zero (ofReal_ne_zero.mpr hx.ne')
  have h2 : a * I * x / (I * x) = a := by
    rw [div_eq_iff hx']
    ring
  have h3 : 1 / (-I * (I * x)) ^ (1 / 2 : â„‚) = 1 / â†‘(x ^ (1 / 2 : â„)) := by
    rw [neg_mul, â† mul_assoc, I_mul_I, neg_one_mul, neg_neg,ofReal_cpow hx.le, ofReal_div,
      ofReal_one, ofReal_ofNat]
  have h4 : -Ï€ * I * (a * I * x) ^ 2 / (I * x) = - (-Ï€ * a ^ 2 * x) := by
    rw [mul_pow, mul_pow, I_sq, div_eq_iff hx']
    ring
  rw [h1, h2, h3, h4, â† mul_assoc, mul_comm (cexp _), mul_assoc _ (cexp _) (cexp _),
    â† Complex.exp_add, neg_add_self, Complex.exp_zero, mul_one, ofReal_div, ofReal_one]

end kernel_defs

section asymp

/-!
## Formulae for the kernels as sums
-/

lemma hasSum_int_evenKernel (a : â„) {t : â„} (ht : 0 < t) :
    HasSum (fun n : â„¤ â†¦ rexp (-Ï€ * (n + a) ^ 2 * t)) (evenKernel a t) := by
  rw [â† hasSum_ofReal, evenKernel_def]
  have (n : â„¤) : â†‘(rexp (-Ï€ * (â†‘n + a) ^ 2 * t)) =
      cexp (-â†‘Ï€ * â†‘a ^ 2 * â†‘t) * jacobiThetaâ‚‚_term n (â†‘a * I * â†‘t) (I * â†‘t) := by
    rw [jacobiThetaâ‚‚_term, â† Complex.exp_add]
    push_cast
    congr
    ring_nf
    simp only [I_sq, mul_neg, neg_mul, mul_one]
  simp only [this]
  exact (hasSum_jacobiThetaâ‚‚_term _ (by rwa [I_mul_im, ofReal_re])).mul_left _

lemma hasSum_int_cosKernel (a : â„) {t : â„} (ht : 0 < t) :
    HasSum (fun n : â„¤ â†¦ cexp (2 * Ï€ * I * a * n) * rexp (-Ï€ * n ^ 2 * t)) â†‘(cosKernel a t) := by
  rw [cosKernel_def a t]
  have (n : â„¤) : cexp (2 * â†‘Ï€ * I * â†‘a * â†‘n) * â†‘(rexp (-Ï€ * â†‘n ^ 2 * t)) =
      jacobiThetaâ‚‚_term n (â†‘a) (I * â†‘t) := by
    rw [jacobiThetaâ‚‚_term, ofReal_exp, â† Complex.exp_add]
    push_cast
    ring_nf
    simp only [I_sq, mul_neg, neg_mul, mul_one, sub_eq_add_neg]
  simp only [this]
  exact hasSum_jacobiThetaâ‚‚_term a (by rwa [I_mul_im, ofReal_re] : 0 < im (I * t))

/-- Modified version of `hasSum_int_evenKernel` omitting the constant term at `âˆ`. -/
lemma hasSum_int_evenKernelâ‚€ (a : â„) {t : â„} (ht : 0 < t) :
    HasSum (fun n : â„¤ â†¦ if n + a = 0 then 0 else rexp (-Ï€ * (n + a) ^ 2 * t))
    (evenKernel a t - if (a : UnitAddCircle) = 0 then 1 else 0) := by
  haveI := Classical.propDecidable -- speed up instance search for `if / then / else`
  simp_rw [AddCircle.coe_eq_zero_iff, zsmul_one]
  split_ifs with h
  Â· obtain âŸ¨k, rflâŸ© := h
    simp_rw [â† Int.cast_add, Int.cast_eq_zero, add_eq_zero_iff_eq_neg]
    simpa only [Int.cast_add, neg_mul, Int.cast_neg, add_left_neg, ne_eq, OfNat.ofNat_ne_zero,
      not_false_eq_true, zero_pow, mul_zero, zero_mul, Real.exp_zero]
      using hasSum_ite_sub_hasSum (hasSum_int_evenKernel (k : â„) ht) (-k)
  Â· suffices âˆ€ (n : â„¤), n + a â‰  0 by simpa [this] using hasSum_int_evenKernel a ht
    contrapose! h
    let âŸ¨n, hnâŸ© := h
    exact âŸ¨-n, by rwa [Int.cast_neg, neg_eq_iff_add_eq_zero]âŸ©

lemma hasSum_int_cosKernelâ‚€ (a : â„) {t : â„} (ht : 0 < t) :
    HasSum (fun n : â„¤ â†¦ if n = 0 then 0 else cexp (2 * Ï€ * I * a * n) * rexp (-Ï€ * n ^ 2 * t))
    (â†‘(cosKernel a t) - 1) := by
  simpa? using hasSum_ite_sub_hasSum (hasSum_int_cosKernel a ht) 0
  says simpa only [neg_mul, ofReal_exp, ofReal_neg, ofReal_mul, ofReal_pow, ofReal_intCast,
    Int.cast_zero, mul_zero, Complex.exp_zero, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true,
    zero_pow, zero_mul, Real.exp_zero, ofReal_one, mul_one] using
    hasSum_ite_sub_hasSum (hasSum_int_cosKernel a ht) 0

lemma hasSum_nat_cosKernelâ‚€ (a : â„) {t : â„} (ht : 0 < t) :
    HasSum (fun n : â„• â†¦ 2 * Real.cos (2 * Ï€ * a * (n + 1)) * rexp (-Ï€ * (n + 1) ^ 2 * t))
    (cosKernel a t - 1) := by
  rw [â† hasSum_ofReal, ofReal_sub, ofReal_one]
  have := (hasSum_int_cosKernel a ht).sum_nat_of_sum_int
  rw [â† hasSum_nat_add_iff' 1] at this
  simp_rw [Finset.sum_range_one, Nat.cast_zero, neg_zero, Int.cast_zero, zero_pow two_ne_zero,
    mul_zero, zero_mul, Complex.exp_zero, Real.exp_zero, ofReal_one, mul_one, Int.cast_neg,
    Int.cast_natCast, neg_sq, â† add_mul, add_sub_assoc, â† sub_sub, sub_self, zero_sub,
    â† sub_eq_add_neg, mul_neg] at this
  refine this.congr_fun fun n â†¦ ?_
  push_cast
  rw [Complex.cos, mul_div_cancelâ‚€ _ two_ne_zero]
  congr 3 <;> ring

/-!
## Asymptotics of the kernels as `t â†’ âˆ`
-/

/-- The function `evenKernel a - L` has exponential decay at `+âˆ`, where `L = 1` if
`a = 0` and `L = 0` otherwise. -/
lemma isBigO_atTop_evenKernel_sub (a : UnitAddCircle) : âˆƒ p : â„, 0 < p âˆ§
    (evenKernel a Â· - (if a = 0 then 1 else 0)) =O[atTop] (rexp <| -p * Â·) := by
  obtain âŸ¨b, _, rflâŸ© := a.eq_coe_Ico
  obtain âŸ¨p, hp, hp'âŸ© := HurwitzKernelBounds.isBigO_atTop_F_int_zero_sub b
  refine âŸ¨p, hp, (EventuallyEq.isBigO ?_).trans hp'âŸ©
  filter_upwards [eventually_gt_atTop 0] with t ht
  simp only [â† (hasSum_int_evenKernel b ht).tsum_eq, HurwitzKernelBounds.F_int,
    HurwitzKernelBounds.f_int, pow_zero, one_mul, Function.Periodic.lift_coe]

/-- The function `cosKernel a - 1` has exponential decay at `+âˆ`, for any `a`. -/
lemma isBigO_atTop_cosKernel_sub (a : UnitAddCircle) :
    âˆƒ p, 0 < p âˆ§ IsBigO atTop (cosKernel a Â· - 1) (fun x â†¦ Real.exp (-p * x)) := by
  induction' a using QuotientAddGroup.induction_on with a
  obtain âŸ¨p, hp, hp'âŸ© := HurwitzKernelBounds.isBigO_atTop_F_nat_zero_sub zero_le_one
  refine âŸ¨p, hp, (Eventually.isBigO ?_).trans (hp'.const_mul_left 2)âŸ©
  simp only [eq_false_intro one_ne_zero, if_false, sub_zero]
  filter_upwards [eventually_gt_atTop 0] with t ht
  rw [â† (hasSum_nat_cosKernelâ‚€ a ht).tsum_eq, HurwitzKernelBounds.F_nat]
  apply tsum_of_norm_bounded ((HurwitzKernelBounds.summable_f_nat 0 1 ht).hasSum.mul_left 2)
  intro n
  rw [norm_mul, norm_mul, norm_two, mul_assoc, mul_le_mul_iff_of_pos_left two_pos,
    norm_of_nonneg (exp_pos _).le, HurwitzKernelBounds.f_nat, pow_zero, one_mul, Real.norm_eq_abs]
  exact mul_le_of_le_one_left (exp_pos _).le (abs_cos_le_one _)

end asymp

section FEPair
/-!
## Construction of a FE-pair
-/

/-- A `WeakFEPair` structure with `f = evenKernel a` and `g = cosKernel a`. -/
def hurwitzEvenFEPair (a : UnitAddCircle) : WeakFEPair â„‚ where
  f := ofReal' âˆ˜ evenKernel a
  g := ofReal' âˆ˜ cosKernel a
  hf_int := (continuous_ofReal.comp_continuousOn (continuousOn_evenKernel a)).locallyIntegrableOn
    measurableSet_Ioi
  hg_int := (continuous_ofReal.comp_continuousOn (continuousOn_cosKernel a)).locallyIntegrableOn
    measurableSet_Ioi
  hk := one_half_pos
  hÎµ := one_ne_zero
  fâ‚€ := if a = 0 then 1 else 0
  hf_top r := by
    let âŸ¨v, hv, hv'âŸ© := isBigO_atTop_evenKernel_sub a
    rw [â† isBigO_norm_left] at hv' âŠ¢
    conv at hv' =>
      enter [2, x]; rw [â† norm_real, ofReal_sub, apply_ite ((â†‘) : â„ â†’ â„‚), ofReal_one, ofReal_zero]
    exact hv'.trans (isLittleO_exp_neg_mul_rpow_atTop hv _).isBigO
  gâ‚€ := 1
  hg_top r := by
    obtain âŸ¨p, hp, hp'âŸ© := isBigO_atTop_cosKernel_sub a
    rw [â† isBigO_norm_left] at hp' âŠ¢
    have (x : â„) : â€–(ofReal' âˆ˜ cosKernel a) x - 1â€– = â€–cosKernel a x - 1â€– := by
      rw [â† norm_real, ofReal_sub, ofReal_one, Function.comp_apply]
    simp only [this]
    exact hp'.trans (isLittleO_exp_neg_mul_rpow_atTop hp _).isBigO
  h_feq x hx := by
    simp_rw [Function.comp_apply, one_mul, smul_eq_mul, â† ofReal_mul,
      evenKernel_functional_equation, one_div x, one_div xâ»Â¹, inv_rpow (le_of_lt hx),
      one_div, inv_inv]

lemma hurwitzEvenFEPair_zero_symm :
    (hurwitzEvenFEPair 0).symm = hurwitzEvenFEPair 0 := by
  unfold hurwitzEvenFEPair WeakFEPair.symm
  congr 1 <;> simp only [evenKernel_eq_cosKernel_of_zero, inv_one, if_true]

lemma hurwitzEvenFEPair_neg (a : UnitAddCircle) : hurwitzEvenFEPair (-a) = hurwitzEvenFEPair a := by
  unfold hurwitzEvenFEPair
  congr 1 <;> simp only [Function.comp_def, evenKernel_neg, cosKernel_neg, neg_eq_zero]

/-!
## Definition of the completed even Hurwitz zeta function
-/

/--
The meromorphic function of `s` which agrees with
`1 / 2 * Gamma (s / 2) * Ï€ ^ (-s / 2) * âˆ‘' (n : â„¤), 1 / |n + a| ^ s` for `1 < re s`.
-/
def completedHurwitzZetaEven (a : UnitAddCircle) (s : â„‚) : â„‚ :=
  ((hurwitzEvenFEPair a).Î› (s / 2)) / 2

/-- The entire function differing from `completedHurwitzZetaEven a s` by a linear combination of
`1 / s` and `1 / (1 - s)`. -/
def completedHurwitzZetaEvenâ‚€ (a : UnitAddCircle) (s : â„‚) : â„‚ :=
  ((hurwitzEvenFEPair a).Î›â‚€ (s / 2)) / 2

lemma completedHurwitzZetaEven_eq (a : UnitAddCircle) (s : â„‚) :
    completedHurwitzZetaEven a s =
    completedHurwitzZetaEvenâ‚€ a s - (if a = 0 then 1 else 0) / s - 1 / (1 - s) := by
  rw [completedHurwitzZetaEven, WeakFEPair.Î›, sub_div, sub_div]
  congr 1
  Â· change completedHurwitzZetaEvenâ‚€ a s - (1 / (s / 2)) â€¢ (if a = 0 then 1 else 0) / 2 =
      completedHurwitzZetaEvenâ‚€ a s - (if a = 0 then 1 else 0) / s
    rw [smul_eq_mul, mul_comm, mul_div_assoc, div_div, div_mul_cancelâ‚€ _ two_ne_zero, mul_one_div]
  Â· change (1 / (â†‘(1 / 2 : â„) - s / 2)) â€¢ 1 / 2 = 1 / (1 - s)
    push_cast
    rw [smul_eq_mul, mul_one, â† sub_div, div_div, div_mul_cancelâ‚€ _ two_ne_zero]

/--
The meromorphic function of `s` which agrees with
`Gamma (s / 2) * Ï€ ^ (-s / 2) * âˆ‘' n : â„•, cos (2 * Ï€ * a * n) / n ^ s` for `1 < re s`.
-/
def completedCosZeta (a : UnitAddCircle) (s : â„‚) : â„‚ :=
  ((hurwitzEvenFEPair a).symm.Î› (s / 2)) / 2

/-- The entire function differing from `completedCosZeta a s` by a linear combination of
`1 / s` and `1 / (1 - s)`. -/
def completedCosZetaâ‚€ (a : UnitAddCircle) (s : â„‚) : â„‚ :=
  ((hurwitzEvenFEPair a).symm.Î›â‚€ (s / 2)) / 2

lemma completedCosZeta_eq (a : UnitAddCircle) (s : â„‚) :
    completedCosZeta a s =
    completedCosZetaâ‚€ a s - 1 / s - (if a = 0 then 1 else 0) / (1 - s) := by
  rw [completedCosZeta, WeakFEPair.Î›, sub_div, sub_div]
  congr 1
  Â· rw [completedCosZetaâ‚€, WeakFEPair.symm, hurwitzEvenFEPair, smul_eq_mul, mul_one, div_div,
      div_mul_cancelâ‚€ _ (two_ne_zero' â„‚)]
  Â· simp_rw [WeakFEPair.symm, hurwitzEvenFEPair, push_cast, inv_one, smul_eq_mul,
      mul_comm _ (if _ then _ else _), mul_div_assoc, div_div, â† sub_div,
      div_mul_cancelâ‚€ _ (two_ne_zero' â„‚), mul_one_div]

/-!
## Parity and functional equations
-/

lemma completedHurwitzZetaEven_neg (a : UnitAddCircle) (s : â„‚) :
    completedHurwitzZetaEven (-a) s = completedHurwitzZetaEven a s := by
  simp only [completedHurwitzZetaEven, hurwitzEvenFEPair_neg]

lemma completedHurwitzZetaEvenâ‚€_neg (a : UnitAddCircle) (s : â„‚) :
    completedHurwitzZetaEvenâ‚€ (-a) s = completedHurwitzZetaEvenâ‚€ a s := by
  simp only [completedHurwitzZetaEvenâ‚€, hurwitzEvenFEPair_neg]

lemma completedCosZeta_neg (a : UnitAddCircle) (s : â„‚) :
    completedCosZeta (-a) s = completedCosZeta a s := by
  simp only [completedCosZeta, hurwitzEvenFEPair_neg]

lemma completedCosZetaâ‚€_neg (a : UnitAddCircle) (s : â„‚) :
    completedCosZetaâ‚€ (-a) s = completedCosZetaâ‚€ a s := by
  simp only [completedCosZetaâ‚€, hurwitzEvenFEPair_neg]

/-- Functional equation for the even Hurwitz zeta function. -/
lemma completedHurwitzZetaEven_one_sub (a : UnitAddCircle) (s : â„‚) :
    completedHurwitzZetaEven a (1 - s) = completedCosZeta a s := by
  rw [completedHurwitzZetaEven, completedCosZeta, sub_div,
    (by norm_num : (1 / 2 : â„‚) = â†‘(1 / 2 : â„)),
    (by rfl : (1 / 2 : â„) = (hurwitzEvenFEPair a).k),
    (hurwitzEvenFEPair a).functional_equation (s / 2),
    (by rfl : (hurwitzEvenFEPair a).Îµ = 1),
    one_smul]

/-- Functional equation for the even Hurwitz zeta function with poles removed. -/
lemma completedHurwitzZetaEvenâ‚€_one_sub (a : UnitAddCircle) (s : â„‚) :
    completedHurwitzZetaEvenâ‚€ a (1 - s) = completedCosZetaâ‚€ a s := by
  rw [completedHurwitzZetaEvenâ‚€, completedCosZetaâ‚€, sub_div,
    (by norm_num : (1 / 2 : â„‚) = â†‘(1 / 2 : â„)),
    (by rfl : (1 / 2 : â„) = (hurwitzEvenFEPair a).k),
    (hurwitzEvenFEPair a).functional_equationâ‚€ (s / 2),
    (by rfl : (hurwitzEvenFEPair a).Îµ = 1),
    one_smul]

/-- Functional equation for the even Hurwitz zeta function (alternative form). -/
lemma completedCosZeta_one_sub (a : UnitAddCircle) (s : â„‚) :
    completedCosZeta a (1 - s) = completedHurwitzZetaEven a s := by
  rw [â† completedHurwitzZetaEven_one_sub, sub_sub_cancel]

/-- Functional equation for the even Hurwitz zeta function with poles removed (alternative form). -/
lemma completedCosZetaâ‚€_one_sub (a : UnitAddCircle) (s : â„‚) :
    completedCosZetaâ‚€ a (1 - s) = completedHurwitzZetaEvenâ‚€ a s := by
  rw [â† completedHurwitzZetaEvenâ‚€_one_sub, sub_sub_cancel]

end FEPair

/-!
## Differentiability and residues
-/

section FEPair

/--
The even Hurwitz completed zeta is differentiable away from `s = 0` and `s = 1` (and also at
`s = 0` if `a â‰  0`)
-/
lemma differentiableAt_completedHurwitzZetaEven
    (a : UnitAddCircle) {s : â„‚} (hs : s â‰  0 âˆ¨ a â‰  0) (hs' : s â‰  1) :
    DifferentiableAt â„‚ (completedHurwitzZetaEven a) s := by
  refine (((hurwitzEvenFEPair a).differentiableAt_Î› ?_ (Or.inl ?_)).comp s
      (differentiableAt_id.div_const _)).div_const _
  Â· rw [div_ne_zero_iff, eq_true_intro two_ne_zero, and_true]
    refine Or.imp (by tauto) (fun ha â†¦ ?_) hs
    simp only [hurwitzEvenFEPair, eq_false_intro ha, if_false]
  Â· change s / 2 â‰  â†‘(1 / 2 : â„)
    rw [ofReal_div, ofReal_one, ofReal_ofNat]
    exact hs' âˆ˜ (div_left_inj' two_ne_zero).mp

lemma differentiable_completedHurwitzZetaEvenâ‚€ (a : UnitAddCircle) :
    Differentiable â„‚ (completedHurwitzZetaEvenâ‚€ a) :=
  ((hurwitzEvenFEPair a).differentiable_Î›â‚€.comp (differentiable_id.div_const _)).div_const _

/-- The difference of two completed even Hurwitz zeta functions is differentiable at `s = 1`. -/
lemma differentiableAt_one_completedHurwitzZetaEven_sub_completedHurwitzZetaEven
    (a b : UnitAddCircle) :
    DifferentiableAt â„‚ (fun s â†¦ completedHurwitzZetaEven a s - completedHurwitzZetaEven b s) 1 := by
  have (s) : completedHurwitzZetaEven a s - completedHurwitzZetaEven b s =
      completedHurwitzZetaEvenâ‚€ a s - completedHurwitzZetaEvenâ‚€ b s -
      ((if a = 0 then 1 else 0) - (if b = 0 then 1 else 0)) / s := by
    simp_rw [completedHurwitzZetaEven_eq, sub_div]
    abel
  rw [funext this]
  refine .sub ?_ <| (differentiable_const _ _).div (differentiable_id _) one_ne_zero
  apply DifferentiableAt.sub <;> apply differentiable_completedHurwitzZetaEvenâ‚€

lemma differentiableAt_completedCosZeta
    (a : UnitAddCircle) {s : â„‚} (hs : s â‰  0) (hs' : s â‰  1 âˆ¨ a â‰  0) :
    DifferentiableAt â„‚ (completedCosZeta a) s := by
  refine (((hurwitzEvenFEPair a).symm.differentiableAt_Î› (Or.inl ?_) ?_).comp s
      (differentiableAt_id.div_const _)).div_const _
  Â· rwa [div_ne_zero_iff, eq_true_intro two_ne_zero, and_true]
  Â· change s / 2 â‰  â†‘(1 / 2 : â„) âˆ¨ (if a = 0 then 1 else 0) = 0
    refine Or.imp (fun h â†¦ ?_) (fun ha â†¦ ?_) hs'
    Â· simpa only [push_cast] using h âˆ˜ (div_left_inj' two_ne_zero).mp
    Â· simp_rw [eq_false_intro ha, if_false]

lemma differentiable_completedCosZetaâ‚€ (a : UnitAddCircle) :
    Differentiable â„‚ (completedCosZetaâ‚€ a) :=
  ((hurwitzEvenFEPair a).symm.differentiable_Î›â‚€.comp (differentiable_id.div_const _)).div_const _

/-- The residue of `completedHurwitzZetaEven a s` at `s = 1` is equal to `1`. -/
lemma completedHurwitzZetaEven_residue_one (a : UnitAddCircle) :
    Tendsto (fun s â†¦ (s - 1) * completedHurwitzZetaEven a s) (ğ“[â‰ ] 1) (ğ“ 1) := by
  have h1 : Tendsto (fun s : â„‚ â†¦ (s - â†‘(1  / 2 : â„)) * _) (ğ“[â‰ ] â†‘(1  / 2 : â„))
    (ğ“ ((1 : â„‚) * (1 : â„‚))) := (hurwitzEvenFEPair a).Î›_residue_k
  simp only [push_cast, one_mul] at h1
  have h2 : Tendsto (fun s : â„‚ â†¦ s / 2) (ğ“[â‰ ] 1) (ğ“[â‰ ] (1 / 2)) :=
    le_of_eq ((Homeomorph.mulRightâ‚€ _ (inv_ne_zero (two_ne_zero' â„‚))).map_punctured_nhds_eq 1)
  refine (h1.comp h2).congr (fun s â†¦ ?_)
  rw [completedHurwitzZetaEven, Function.comp_apply, â† sub_div, div_mul_eq_mul_div, mul_div_assoc]

/-- The residue of `completedHurwitzZetaEven a s` at `s = 0` is equal to `-1` if `a = 0`, and `0`
otherwise. -/
lemma completedHurwitzZetaEven_residue_zero (a : UnitAddCircle) :
    Tendsto (fun s â†¦ s * completedHurwitzZetaEven a s) (ğ“[â‰ ] 0) (ğ“ (if a = 0 then -1 else 0)) := by
  have h1 : Tendsto (fun s : â„‚ â†¦ s * _) (ğ“[â‰ ] 0)
    (ğ“ (-(if a = 0 then 1 else 0))) := (hurwitzEvenFEPair a).Î›_residue_zero
  have : -(if a = 0 then (1 : â„‚) else 0) = (if a = 0 then -1 else 0) := by { split_ifs <;> simp }
  simp only [this, push_cast, one_mul] at h1
  have h2 : Tendsto (fun s : â„‚ â†¦ s / 2) (ğ“[â‰ ] 0) (ğ“[â‰ ] (0 / 2)) :=
    le_of_eq ((Homeomorph.mulRightâ‚€ _ (inv_ne_zero (two_ne_zero' â„‚))).map_punctured_nhds_eq 0)
  rw [zero_div] at h2
  refine (h1.comp h2).congr (fun s â†¦ ?_)
  rw [completedHurwitzZetaEven, Function.comp_apply, div_mul_eq_mul_div, mul_div_assoc]

lemma completedCosZeta_residue_zero (a : UnitAddCircle) :
    Tendsto (fun s â†¦ s * completedCosZeta a s) (ğ“[â‰ ] 0) (ğ“ (-1)) := by
  have h1 : Tendsto (fun s : â„‚ â†¦ s * _) (ğ“[â‰ ] 0)
    (ğ“ (-1)) := (hurwitzEvenFEPair a).symm.Î›_residue_zero
  have h2 : Tendsto (fun s : â„‚ â†¦ s / 2) (ğ“[â‰ ] 0) (ğ“[â‰ ] (0 / 2)) :=
    le_of_eq ((Homeomorph.mulRightâ‚€ _ (inv_ne_zero (two_ne_zero' â„‚))).map_punctured_nhds_eq 0)
  rw [zero_div] at h2
  refine (h1.comp h2).congr (fun s â†¦ ?_)
  rw [completedCosZeta, Function.comp_apply, div_mul_eq_mul_div, mul_div_assoc]

end FEPair

/-!
## Relation to the Dirichlet series for `1 < re s`
-/

/-- Formula for `completedCosZeta` as a Dirichlet series in the convergence range
(first version, with sum over `â„¤`). -/
lemma hasSum_int_completedCosZeta (a : â„) {s : â„‚} (hs : 1 < re s) :
    HasSum (fun n : â„¤ â†¦ Gammaâ„ s * cexp (2 * Ï€ * I * a * n) / (â†‘|n| : â„‚) ^ s / 2)
    (completedCosZeta a s) := by
  let c (n : â„¤) : â„‚ := cexp (2 * Ï€ * I * a * n) / 2
  have hF t (ht : 0 < t) : HasSum (fun n : â„¤ â†¦ if n = 0 then 0 else c n * rexp (-Ï€ * n ^ 2 * t))
      ((cosKernel a t - 1) / 2) := by
    refine ((hasSum_int_cosKernelâ‚€ a ht).div_const 2).congr_fun fun n â†¦ ?_
    split_ifs with h <;>
      simp only [zero_div, c, div_mul_eq_mul_div]
  simp only [â† Int.cast_eq_zero (Î± := â„)] at hF
  rw [show completedCosZeta a s = mellin (fun t â†¦ (cosKernel a t - 1 : â„‚) / 2) (s / 2) by
    rw [mellin_div_const, completedCosZeta]
    congr 1
    refine ((hurwitzEvenFEPair a).symm.hasMellin (?_ : 1 / 2 < (s / 2).re)).2.symm
    rwa [div_ofNat_re, div_lt_div_right two_pos]]
  refine (hasSum_mellin_pi_mul_sq (zero_lt_one.trans hs) hF ?_).congr_fun fun n â†¦ ?_
  Â· apply (((summable_one_div_int_add_rpow 0 s.re).mpr hs).div_const 2).of_norm_bounded
    intro i
    simp only [c, (by { push_cast; ring } : 2 * Ï€ * I * a * i = â†‘(2 * Ï€ * a * i) * I), norm_div,
      RCLike.norm_ofNat, norm_norm, Complex.norm_exp_ofReal_mul_I, add_zero, norm_one,
      norm_of_nonneg (by positivity : 0 â‰¤ |(i : â„)| ^ s.re), div_right_comm, le_rfl]
  Â· simp only [c, Int.cast_eq_zero, â† Int.cast_abs, ofReal_intCast, div_right_comm, mul_div_assoc]

/-- Formula for `completedCosZeta` as a Dirichlet series in the convergence range
(second version, with sum over `â„•`). -/
lemma hasSum_nat_completedCosZeta (a : â„) {s : â„‚} (hs : 1 < re s) :
    HasSum (fun n : â„• â†¦ if n = 0 then 0 else Gammaâ„ s * Real.cos (2 * Ï€ * a * n) / (n : â„‚) ^ s)
    (completedCosZeta a s) := by
  have hs' : s â‰  0 := (not_lt.mpr zero_le_one <| zero_re â–¸ Â· â–¸ hs)
  have aux : ((|0| : â„¤) : â„‚) ^ s = 0 := by rw [abs_zero, Int.cast_zero, zero_cpow hs']
  have hint := (hasSum_int_completedCosZeta a hs).sum_nat_of_sum_int
  rw [aux, div_zero, zero_div, add_zero] at hint
  refine hint.congr_fun fun n â†¦ ?_
  split_ifs with h
  Â· simp only [h, Nat.cast_zero, aux, div_zero, zero_div, neg_zero, zero_add]
  Â· simp only [ofReal_cos, ofReal_mul, ofReal_ofNat, ofReal_natCast, Complex.cos,
      show 2 * Ï€ * a * n * I = 2 * Ï€ * I * a * n by ring, neg_mul, mul_div_assoc,
      div_right_comm _ (2 : â„‚), Int.cast_natCast, Nat.abs_cast, Int.cast_neg, mul_neg, abs_neg, â†
      mul_add, â† add_div]

/-- Formula for `completedHurwitzZetaEven` as a Dirichlet series in the convergence range. -/
lemma hasSum_int_completedHurwitzZetaEven (a : â„) {s : â„‚} (hs : 1 < re s) :
    HasSum (fun n : â„¤ â†¦ Gammaâ„ s / (â†‘|n + a| : â„‚) ^ s / 2) (completedHurwitzZetaEven a s) := by
  have hF (t : â„) (ht : 0 < t) : HasSum (fun n : â„¤ â†¦ if n + a = 0 then 0
      else (1 / 2 : â„‚) * rexp (-Ï€ * (n + a) ^ 2 * t)) ((evenKernel a t - _) / 2) := by
    refine (ofReal_sub .. â–¸ (hasSum_ofReal.mpr (hasSum_int_evenKernelâ‚€ a ht)).div_const
      2).congr_fun fun n â†¦ ?_
    split_ifs
    Â· rw [ofReal_zero, zero_div]
    Â· rw [mul_comm, mul_one_div]
  rw [show completedHurwitzZetaEven a s = mellin (fun t â†¦ ((evenKernel (â†‘a) t : â„‚) -
        â†‘(if (a : UnitAddCircle) = 0 then 1 else 0 : â„)) / 2) (s / 2) by
    simp_rw [mellin_div_const, apply_ite ofReal', ofReal_one, ofReal_zero]
    refine congr_arg (Â· / 2) ((hurwitzEvenFEPair a).hasMellin (?_ : 1 / 2 < (s / 2).re)).2.symm
    rwa [div_ofNat_re, div_lt_div_right two_pos]]
  refine (hasSum_mellin_pi_mul_sq (zero_lt_one.trans hs) hF ?_).congr_fun fun n â†¦ ?_
  Â· simp_rw [â† mul_one_div â€–_â€–]
    apply Summable.mul_left
    rwa [summable_one_div_int_add_rpow]
  Â· rw [mul_one_div, div_right_comm]

/-!
## The un-completed even Hurwitz zeta
-/

/-- Technical lemma which will give us differentiability of Riemann zeta at `s = 0`. -/
lemma differentiableAt_update_of_residue
    {Î› : â„‚ â†’ â„‚} (hf : âˆ€ (s : â„‚) (_ : s â‰  0) (_ : s â‰  1), DifferentiableAt â„‚ Î› s)
    {L : â„‚} (h_lim : Tendsto (fun s â†¦ s * Î› s) (ğ“[â‰ ] 0) (ğ“ L)) (s : â„‚) (hs' : s â‰  1) :
    DifferentiableAt â„‚ (Function.update (fun s â†¦ Î› s / Gammaâ„ s) 0 (L / 2)) s := by
  have claim (t) (ht : t â‰  0) (ht' : t â‰  1) : DifferentiableAt â„‚ (fun u : â„‚ â†¦ Î› u / Gammaâ„ u) t :=
    (hf t ht ht').mul differentiable_Gammaâ„_inv.differentiableAt
  have claim2 : Tendsto (fun s : â„‚ â†¦ Î› s / Gammaâ„ s) (ğ“[â‰ ] 0) (ğ“ <| L / 2) := by
    refine Tendsto.congr' ?_ (h_lim.div Gammaâ„_residue_zero two_ne_zero)
    filter_upwards [self_mem_nhdsWithin] with s (hs : s â‰  0)
    rw [Pi.div_apply, â† div_div, mul_div_cancel_leftâ‚€ _ hs]
  rcases ne_or_eq s 0 with hs | rfl
  Â· -- Easy case : `s â‰  0`
    refine (claim s hs hs').congr_of_eventuallyEq ?_
    filter_upwards [isOpen_compl_singleton.mem_nhds hs] with x hx
    simp only [Function.update_noteq hx]
  Â· -- Hard case : `s = 0`
    simp_rw [â† claim2.limUnder_eq]
    have S_nhds : {(1 : â„‚)}á¶œ âˆˆ ğ“ (0 : â„‚) := isOpen_compl_singleton.mem_nhds hs'
    refine ((Complex.differentiableOn_update_limUnder_of_isLittleO S_nhds
      (fun t ht â†¦ (claim t ht.2 ht.1).differentiableWithinAt) ?_) 0 hs').differentiableAt S_nhds
    simp only [Gammaâ„, zero_div, div_zero, Complex.Gamma_zero, mul_zero, cpow_zero, sub_zero]
    -- Remains to show completed zeta is `o (s ^ (-1))` near 0.
    refine (isBigO_const_of_tendsto claim2 <| one_ne_zero' â„‚).trans_isLittleO ?_
    rw [isLittleO_iff_tendsto']
    Â· exact Tendsto.congr (fun x â†¦ by rw [â† one_div, one_div_one_div]) nhdsWithin_le_nhds
    Â· exact eventually_of_mem self_mem_nhdsWithin fun x hx hx' â†¦ (hx <| inv_eq_zero.mp hx').elim

/-- The even part of the Hurwitz zeta function, i.e. the meromorphic function of `s` which agrees
with `1 / 2 * âˆ‘' (n : â„¤), 1 / |n + a| ^ s` for `1 < re s`-/
noncomputable def hurwitzZetaEven (a : UnitAddCircle) :=
  Function.update (fun s â†¦ completedHurwitzZetaEven a s / Gammaâ„ s)
  0 (if a = 0 then -1 / 2 else 0)

lemma hurwitzZetaEven_def_of_ne_or_ne {a : UnitAddCircle} {s : â„‚} (h : a â‰  0 âˆ¨ s â‰  0) :
    hurwitzZetaEven a s = completedHurwitzZetaEven a s / Gammaâ„ s := by
  rw [hurwitzZetaEven]
  rcases ne_or_eq s 0 with h | rfl
  Â· rw [Function.update_noteq h]
  Â· simpa only [Gammaâ„, Function.update_same, neg_zero, zero_div, cpow_zero, Complex.Gamma_zero,
    mul_zero, div_zero, ite_eq_right_iff, div_eq_zero_iff, neg_eq_zero, one_ne_zero,
    OfNat.ofNat_ne_zero, or_self, imp_false, ne_eq, not_true_eq_false, or_false] using h

lemma hurwitzZetaEven_apply_zero (a : UnitAddCircle) :
    hurwitzZetaEven a 0 = if a = 0 then -1 / 2 else 0 :=
  Function.update_same _ _ _

lemma hurwitzZetaEven_neg (a : UnitAddCircle) (s : â„‚) :
    hurwitzZetaEven (-a) s = hurwitzZetaEven a s := by
  simp_rw [hurwitzZetaEven, neg_eq_zero, completedHurwitzZetaEven_neg]

/-- The trivial zeroes of the even Hurwitz zeta function. -/
theorem hurwitzZetaEven_neg_two_mul_nat_add_one (a : UnitAddCircle) (n : â„•) :
    hurwitzZetaEven a (-2 * (n + 1)) = 0 := by
  have : (-2 : â„‚) * (n + 1) â‰  0 :=
    mul_ne_zero (neg_ne_zero.mpr two_ne_zero) (Nat.cast_add_one_ne_zero n)
  rw [hurwitzZetaEven, Function.update_noteq this,
    Gammaâ„_eq_zero_iff.mpr âŸ¨n + 1, by rw [neg_mul, Nat.cast_add_one]âŸ©, div_zero]

/-- The Hurwitz zeta function is differentiable everywhere except at `s = 1`. This is true
even in the delicate case `a = 0` and `s = 0` (where the completed zeta has a pole, but this is
cancelled out by the Gamma factor). -/
lemma differentiableAt_hurwitzZetaEven (a : UnitAddCircle) {s : â„‚} (hs' : s â‰  1) :
    DifferentiableAt â„‚ (hurwitzZetaEven a) s := by
  have := differentiableAt_update_of_residue
    (fun t ht ht' â†¦ differentiableAt_completedHurwitzZetaEven a (Or.inl ht) ht')
    (completedHurwitzZetaEven_residue_zero a) s hs'
  simp_rw [div_eq_mul_inv, ite_mul, zero_mul, â† div_eq_mul_inv] at this
  exact this

lemma hurwitzZetaEven_residue_one (a : UnitAddCircle) :
    Tendsto (fun s â†¦ (s - 1) * hurwitzZetaEven a s) (ğ“[â‰ ] 1) (ğ“ 1) := by
  have : Tendsto (fun s â†¦ (s - 1) * completedHurwitzZetaEven a s / Gammaâ„ s) (ğ“[â‰ ] 1) (ğ“ 1) := by
    simpa only [Gammaâ„_one, inv_one, mul_one] using (completedHurwitzZetaEven_residue_one a).mul
      <| (differentiable_Gammaâ„_inv.continuous.tendsto _).mono_left nhdsWithin_le_nhds
  refine this.congr' ?_
  filter_upwards [eventually_ne_nhdsWithin one_ne_zero] with s hs
  simp_rw [hurwitzZetaEven_def_of_ne_or_ne (Or.inr hs), mul_div_assoc]

lemma differentiableAt_hurwitzZetaEven_sub_one_div (a : UnitAddCircle) :
    DifferentiableAt â„‚ (fun s â†¦ hurwitzZetaEven a s - 1 / (s - 1) / Gammaâ„ s) 1 := by
  suffices DifferentiableAt â„‚
      (fun s â†¦ completedHurwitzZetaEven a s / Gammaâ„ s - 1 / (s - 1) / Gammaâ„ s) 1 by
    apply this.congr_of_eventuallyEq
    filter_upwards [eventually_ne_nhds one_ne_zero] with x hx
    rw [hurwitzZetaEven, Function.update_noteq hx]
  simp_rw [â† sub_div, div_eq_mul_inv _ (Gammaâ„ _)]
  refine DifferentiableAt.mul ?_ differentiable_Gammaâ„_inv.differentiableAt
  simp_rw [completedHurwitzZetaEven_eq, sub_sub, add_assoc]
  conv => enter [2, s, 2]; rw [â† neg_sub, div_neg, neg_add_self, add_zero]
  exact (differentiable_completedHurwitzZetaEvenâ‚€ a _).sub
    <| (differentiableAt_const _).div differentiableAt_id one_ne_zero

/-- Expression for `hurwitzZetaEven a 1` as a limit. (Mathematically `hurwitzZetaEven a 1` is
undefined, but our construction assigns some value to it; this lemma is mostly of interest for
determining what that value is). -/
lemma tendsto_hurwitzZetaEven_sub_one_div_nhds_one (a : UnitAddCircle) :
    Tendsto (fun s â†¦ hurwitzZetaEven a s - 1 / (s - 1) / Gammaâ„ s) (ğ“ 1)
    (ğ“ (hurwitzZetaEven a 1)) := by
  simpa only [one_div, sub_self, div_zero, Gammaâ„_one, div_one, sub_zero] using
    (differentiableAt_hurwitzZetaEven_sub_one_div a).continuousAt.tendsto

lemma differentiable_hurwitzZetaEven_sub_hurwitzZetaEven (a b : UnitAddCircle) :
    Differentiable â„‚ (fun s â†¦ hurwitzZetaEven a s - hurwitzZetaEven b s) := by
  intro z
  rcases ne_or_eq z 1 with hz | rfl
  Â· exact (differentiableAt_hurwitzZetaEven a hz).sub (differentiableAt_hurwitzZetaEven b hz)
  Â· -- NB. This can be written more tidy with `convert`, but the `convert` version is 3x slower, as
    -- it spends 877 TC synthesis steps vainly trying to infer `Subsingleton (â„‚ â†’ â„‚)`.
    have (s) : (hurwitzZetaEven a s - hurwitzZetaEven b s) = ((hurwitzZetaEven a s -
        1 / (s - 1) / Gammaâ„ s) - (hurwitzZetaEven b s - 1 / (s - 1) / Gammaâ„ s)) := by abel
    exact funext this â–¸ (differentiableAt_hurwitzZetaEven_sub_one_div a).sub
      (differentiableAt_hurwitzZetaEven_sub_one_div b)

/--
Formula for `hurwitzZetaEven` as a Dirichlet series in the convergence range, with sum over `â„¤`.
-/
lemma hasSum_int_hurwitzZetaEven (a : â„) {s : â„‚} (hs : 1 < re s) :
    HasSum (fun n : â„¤ â†¦ 1 / (â†‘|n + a| : â„‚) ^ s / 2) (hurwitzZetaEven a s) := by
  rw [hurwitzZetaEven, Function.update_noteq (not_lt.mpr zero_le_one <| zero_re â–¸ Â· â–¸ hs)]
  have := (hasSum_int_completedHurwitzZetaEven a hs).div_const (Gammaâ„ s)
  exact this.congr_fun fun n â†¦ by simp only [div_right_comm _ _ (Gammaâ„ _),
    div_self (Gammaâ„_ne_zero_of_re_pos (zero_lt_one.trans hs))]

/-- Formula for `hurwitzZetaEven` as a Dirichlet series in the convergence range, with sum over `â„•`
(version with absolute values) -/
lemma hasSum_nat_hurwitzZetaEven (a : â„) {s : â„‚} (hs : 1 < re s) :
    HasSum (fun n : â„• â†¦ (1 / (â†‘|n + a| : â„‚) ^ s + 1 / (â†‘|n + 1 - a| : â„‚) ^ s) / 2)
    (hurwitzZetaEven a s) := by
  refine (hasSum_int_hurwitzZetaEven a hs).nat_add_neg_add_one.congr_fun fun n â†¦ ?_
  simp only [â† abs_neg (n + 1 - a), neg_sub', sub_neg_eq_add, add_div, Int.cast_natCast,
    Int.cast_neg, Int.cast_add, Int.cast_one]

/-- Formula for `hurwitzZetaEven` as a Dirichlet series in the convergence range, with sum over `â„•`
(version without absolute values, assuming `a âˆˆ Icc 0 1`) -/
lemma hasSum_nat_hurwitzZetaEven_of_mem_Icc {a : â„} (ha : a âˆˆ Icc 0 1) {s : â„‚} (hs : 1 < re s) :
    HasSum (fun n : â„• â†¦ (1 / (n + a : â„‚) ^ s + 1 / (n + 1 - a : â„‚) ^ s) / 2)
    (hurwitzZetaEven a s) := by
  refine (hasSum_nat_hurwitzZetaEven a hs).congr_fun fun n â†¦ ?_
  congr 2 <;>
  rw [_root_.abs_of_nonneg (by linarith [ha.1, ha.2])] <;>
  simp only [one_div, ofReal_sub, ofReal_add, ofReal_natCast, ofReal_one]

/-!
## The un-completed cosine zeta
-/

/-- The cosine zeta function, i.e. the meromorphic function of `s` which agrees
with `âˆ‘' (n : â„•), cos (2 * Ï€ * a * n) / n ^ s` for `1 < re s`. -/
noncomputable def cosZeta (a : UnitAddCircle) :=
  Function.update (fun s : â„‚ â†¦ completedCosZeta a s / Gammaâ„ s) 0 (-1 / 2)

lemma cosZeta_apply_zero (a : UnitAddCircle) : cosZeta a 0 = -1 / 2 :=
  Function.update_same _ _ _

lemma cosZeta_neg (a : UnitAddCircle) (s : â„‚) :
    cosZeta (-a) s = cosZeta a s := by
  simp_rw [cosZeta, completedCosZeta_neg]

/-- The trivial zeroes of the cosine zeta function. -/
theorem cosZeta_neg_two_mul_nat_add_one (a : UnitAddCircle) (n : â„•) :
    cosZeta a (-2 * (n + 1)) = 0 := by
  have : (-2 : â„‚) * (n + 1) â‰  0 :=
    mul_ne_zero (neg_ne_zero.mpr two_ne_zero) (Nat.cast_add_one_ne_zero n)
  rw [cosZeta, Function.update_noteq this,
    Gammaâ„_eq_zero_iff.mpr âŸ¨n + 1, by rw [neg_mul, Nat.cast_add_one]âŸ©, div_zero]

/-- The cosine zeta function is differentiable everywhere, except at `s = 1` if `a = 0`. -/
lemma differentiableAt_cosZeta (a : UnitAddCircle) {s : â„‚} (hs' : s â‰  1 âˆ¨ a â‰  0) :
    DifferentiableAt â„‚ (cosZeta a) s := by
  rcases ne_or_eq s 1 with hs' | rfl
  Â· exact differentiableAt_update_of_residue (fun _ ht ht' â†¦
      differentiableAt_completedCosZeta a ht (Or.inl ht')) (completedCosZeta_residue_zero a) s hs'
  Â· apply ((differentiableAt_completedCosZeta a one_ne_zero hs').mul
      (differentiable_Gammaâ„_inv.differentiableAt)).congr_of_eventuallyEq
    filter_upwards [isOpen_compl_singleton.mem_nhds one_ne_zero] with x hx
    simp_rw [cosZeta, Function.update_noteq hx, div_eq_mul_inv]

/-- If `a â‰  0` then the cosine zeta function is entire. -/
lemma differentiable_cosZeta_of_ne_zero {a : UnitAddCircle} (ha : a â‰  0) :
    Differentiable â„‚ (cosZeta a) :=
  fun _ â†¦ differentiableAt_cosZeta a (Or.inr ha)

/-- Formula for `cosZeta` as a Dirichlet series in the convergence range, with sum over `â„¤`. -/
lemma hasSum_int_cosZeta (a : â„) {s : â„‚} (hs : 1 < re s) :
    HasSum (fun n : â„¤ â†¦ cexp (2 * Ï€ * I * a * n) / â†‘|n| ^ s / 2) (cosZeta a s) := by
  rw [cosZeta, Function.update_noteq (not_lt.mpr zero_le_one <| zero_re â–¸ Â· â–¸ hs)]
  refine ((hasSum_int_completedCosZeta a hs).div_const (Gammaâ„ s)).congr_fun fun n â†¦ ?_
  rw [mul_div_assoc _ (cexp _), div_right_comm _ (2 : â„‚),
    mul_div_cancel_leftâ‚€ _ (Gammaâ„_ne_zero_of_re_pos (zero_lt_one.trans hs))]

/-- Formula for `cosZeta` as a Dirichlet series in the convergence range, with sum over `â„•`. -/
lemma hasSum_nat_cosZeta (a : â„) {s : â„‚} (hs : 1 < re s) :
    HasSum (fun n : â„• â†¦ Real.cos (2 * Ï€ * a * n) / (n : â„‚) ^ s) (cosZeta a s) := by
  have hs' : s â‰  0 := (fun h â†¦ (not_lt.mpr zero_le_one) ((zero_re â–¸ h â–¸ hs)))
  have := (hasSum_int_cosZeta a hs).sum_nat_of_sum_int
  simp_rw [abs_neg, Int.cast_neg, Nat.abs_cast, Int.cast_natCast, mul_neg,
    abs_zero, Int.cast_zero, zero_cpow hs', div_zero, zero_div, add_zero, â† add_div,
    div_right_comm _ _ (2 : â„‚)] at this
  simp_rw [push_cast, Complex.cos, neg_mul]
  exact this.congr_fun fun n â†¦ by rw [show 2 * Ï€ * a * n * I = 2 * Ï€ * I * a * n by ring]

/-- Reformulation of `hasSum_nat_cosZeta` using `LSeriesHasSum`. -/
lemma LSeriesHasSum_cos (a : â„) {s : â„‚} (hs : 1 < re s) :
    LSeriesHasSum (Real.cos <| 2 * Ï€ * a * Â·) s (cosZeta a s) := by
  refine (hasSum_nat_cosZeta a hs).congr_fun (fun n â†¦ ?_)
  rcases eq_or_ne n 0 with rfl | hn
  Â· rw [LSeries.term_zero, Nat.cast_zero, Nat.cast_zero,
      zero_cpow (fun h â†¦ (not_lt.mpr zero_le_one) ((zero_re â–¸ h â–¸ hs))), div_zero]
  Â· apply LSeries.term_of_ne_zero hn

/-!
## Functional equations for the un-completed zetas
-/

/-- If `s` is not in `-â„•`, and either `a â‰  0` or `s â‰  1`, then
`hurwitzZetaEven a (1 - s)` is an explicit multiple of `cosZeta s`. -/
lemma hurwitzZetaEven_one_sub (a : UnitAddCircle) {s : â„‚}
    (hs : âˆ€ (n : â„•), s â‰  -n) (hs' : a â‰  0 âˆ¨ s â‰  1) :
    hurwitzZetaEven a (1 - s) = 2 * (2 * Ï€) ^ (-s) * Gamma s * cos (Ï€ * s / 2) * cosZeta a s := by
  have : hurwitzZetaEven a (1 - s) = completedHurwitzZetaEven a (1 - s) * (Gammaâ„ (1 - s))â»Â¹ := by
    rw [hurwitzZetaEven_def_of_ne_or_ne, div_eq_mul_inv]
    simpa [sub_eq_zero, eq_comm (a := s)] using hs'
  rw [this, completedHurwitzZetaEven_one_sub, inv_Gammaâ„_one_sub hs, cosZeta,
    Function.update_noteq (by simpa using hs 0), â† Gammaâ„‚]
  generalize Gammaâ„‚ s * cos (Ï€ * s / 2) = A -- speeds up ring_nf call
  ring_nf

/-- If `s` is not of the form `1 - n` for `n âˆˆ â„•`, then `cosZeta a (1 - s)` is an explicit
multiple of `hurwitzZetaEven s`. -/
lemma cosZeta_one_sub (a : UnitAddCircle) {s : â„‚} (hs : âˆ€ (n : â„•), s â‰  1 - n) :
    cosZeta a (1 - s) = 2 * (2 * Ï€) ^ (-s) * Gamma s * cos (Ï€ * s / 2) * hurwitzZetaEven a s := by
  rw [â† Gammaâ„‚]
  have : cosZeta a (1 - s) = completedCosZeta a (1 - s) * (Gammaâ„ (1 - s))â»Â¹ := by
    rw [cosZeta, Function.update_noteq, div_eq_mul_inv]
    simpa [sub_eq_zero] using (hs 0).symm
  rw [this, completedCosZeta_one_sub, inv_Gammaâ„_one_sub (fun n â†¦ by simpa using hs (n + 1)),
    hurwitzZetaEven_def_of_ne_or_ne (Or.inr (by simpa using hs 1))]
  generalize Gammaâ„‚ s * cos (Ï€ * s / 2) = A -- speeds up ring_nf call
  ring_nf
