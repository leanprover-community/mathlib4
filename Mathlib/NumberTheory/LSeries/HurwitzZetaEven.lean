/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.NumberTheory.LSeries.AbstractFuncEq
import Mathlib.NumberTheory.ModularForms.JacobiTheta.Bounds
import Mathlib.Analysis.SpecialFunctions.Gamma.Deligne
import Mathlib.NumberTheory.LSeries.MellinEqDirichlet
import Mathlib.NumberTheory.LSeries.Basic
import Mathlib.Analysis.Complex.RemovableSingularity

/-!
# Even Hurwitz zeta functions

In this file we study the functions on `‚ÑÇ` which are the meromorphic continuation of the following
series (convergent for `1 < re s`), where `a ‚àà ‚Ñù` is a parameter:

`hurwitzZetaEven a s = 1 / 2 * ‚àë' n : ‚Ñ§, 1 / |n + a| ^ s`

and

`cosZeta a s = ‚àë' n : ‚Ñï, cos (2 * œÄ * a * n) / |n| ^ s`.

Note that the term for `n = -a` in the first sum is omitted if `a` is an integer, and the term for
`n = 0` is omitted in the second sum (always).

Of course, we cannot *define* these functions by the above formulae (since existence of the
meromorphic continuation is not at all obvious); we in fact construct them as Mellin transforms of
various versions of the Jacobi theta function.

We also define completed versions of these functions with nicer functional equations (satisfying
`completedHurwitzZetaEven a s = Gamma‚Ñù s * hurwitzZetaEven a s`, and similarly for `cosZeta`); and
modified versions with a subscript `0`, which are entire functions differing from the above by
multiples of `1 / s` and `1 / (1 - s)`.

## Main definitions and theorems
* `hurwitzZetaEven` and `cosZeta`: the zeta functions
* `completedHurwitzZetaEven` and `completedCosZeta`: completed variants
* `differentiableAt_hurwitzZetaEven` and `differentiableAt_cosZeta`:
  differentiability away from `s = 1`
* `completedHurwitzZetaEven_one_sub`: the functional equation
  `completedHurwitzZetaEven a (1 - s) = completedCosZeta a s`
* `hasSum_int_hurwitzZetaEven` and `hasSum_nat_cosZeta`: relation between the zeta functions and
  the corresponding Dirichlet series for `1 < re s`.
-/
noncomputable section

open Complex Filter Topology Asymptotics Real Set MeasureTheory

namespace HurwitzZeta

section kernel_defs
/-!
## Definitions and elementary properties of kernels
-/

/-- Even Hurwitz zeta kernel (function whose Mellin transform will be the even part of the
completed Hurwit zeta function). See `evenKernel_def` for the defining formula, and
`hasSum_int_evenKernel` for an expression as a sum over `‚Ñ§`. -/
@[irreducible] def evenKernel (a : UnitAddCircle) (x : ‚Ñù) : ‚Ñù :=
  (show Function.Periodic
    (fun Œæ : ‚Ñù ‚Ü¶ rexp (-œÄ * Œæ ^ 2 * x) * re (jacobiTheta‚ÇÇ (Œæ * I * x) (I * x))) 1 by
      intro Œæ
      simp only [ofReal_add, ofReal_one, add_mul, one_mul, jacobiTheta‚ÇÇ_add_left']
      have : cexp (-‚ÜëœÄ * I * ((I * ‚Üëx) + 2 * (‚ÜëŒæ * I * ‚Üëx))) = rexp (œÄ * (x + 2 * Œæ * x)) := by
        ring_nf
        simp [I_sq]
      rw [this, re_ofReal_mul, ‚Üê mul_assoc, ‚Üê Real.exp_add]
      congr
      ring).lift a

lemma evenKernel_def (a x : ‚Ñù) :
    ‚Üë(evenKernel ‚Üëa x) = cexp (-œÄ * a ^ 2 * x) * jacobiTheta‚ÇÇ (a * I * x) (I * x) := by
  simp [evenKernel, re_eq_add_conj, jacobiTheta‚ÇÇ_conj, ‚Üê mul_two,
    mul_div_cancel_right‚ÇÄ _ (two_ne_zero' ‚ÑÇ)]

/-- For `x ‚â§ 0` the defining sum diverges, so the kernel is 0. -/
lemma evenKernel_undef (a : UnitAddCircle) {x : ‚Ñù} (hx : x ‚â§ 0) : evenKernel a x = 0 := by
  induction a using QuotientAddGroup.induction_on with
  | H a' => simp [‚Üê ofReal_inj, evenKernel_def, jacobiTheta‚ÇÇ_undef _ (by simpa : (I * ‚Üëx).im ‚â§ 0)]

/-- Cosine Hurwitz zeta kernel. See `cosKernel_def` for the defining formula, and
`hasSum_int_cosKernel` for expression as a sum. -/
@[irreducible] def cosKernel (a : UnitAddCircle) (x : ‚Ñù) : ‚Ñù :=
  (show Function.Periodic (fun Œæ : ‚Ñù ‚Ü¶ re (jacobiTheta‚ÇÇ Œæ (I * x))) 1 by
    intro Œæ; simp [jacobiTheta‚ÇÇ_add_left]).lift a

lemma cosKernel_def (a x : ‚Ñù) : ‚Üë(cosKernel ‚Üëa x) = jacobiTheta‚ÇÇ a (I * x) := by
  simp [cosKernel, re_eq_add_conj, jacobiTheta‚ÇÇ_conj, ‚Üê mul_two,
    mul_div_cancel_right‚ÇÄ _ (two_ne_zero' ‚ÑÇ)]

lemma cosKernel_undef (a : UnitAddCircle) {x : ‚Ñù} (hx : x ‚â§ 0) : cosKernel a x = 0 := by
  induction a using QuotientAddGroup.induction_on with
  | H => simp [‚Üê ofReal_inj, cosKernel_def, jacobiTheta‚ÇÇ_undef _ (by simpa : (I * ‚Üëx).im ‚â§ 0)]

/-- For `a = 0`, both kernels agree. -/
lemma evenKernel_eq_cosKernel_of_zero : evenKernel 0 = cosKernel 0 := by
  ext1 x
  simp [‚Üê QuotientAddGroup.mk_zero, ‚Üê ofReal_inj, evenKernel_def, cosKernel_def]

@[simp]
lemma evenKernel_neg (a : UnitAddCircle) (x : ‚Ñù) : evenKernel (-a) x = evenKernel a x := by
  induction a using QuotientAddGroup.induction_on with
  | H => simp [‚Üê QuotientAddGroup.mk_neg, ‚Üê ofReal_inj, evenKernel_def, jacobiTheta‚ÇÇ_neg_left]

@[simp]
lemma cosKernel_neg (a : UnitAddCircle) (x : ‚Ñù) : cosKernel (-a) x = cosKernel a x := by
  induction a using QuotientAddGroup.induction_on with
  | H => simp [‚Üê QuotientAddGroup.mk_neg, ‚Üê ofReal_inj, cosKernel_def]

lemma continuousOn_evenKernel (a : UnitAddCircle) : ContinuousOn (evenKernel a) (Ioi 0) := by
  induction a using QuotientAddGroup.induction_on with | H a' =>
  apply continuous_re.comp_continuousOn (f := fun x ‚Ü¶ (evenKernel a' x : ‚ÑÇ))
  simp only [evenKernel_def]
  refine continuousOn_of_forall_continuousAt (fun x hx ‚Ü¶ .mul (by fun_prop) ?_)
  exact (continuousAt_jacobiTheta‚ÇÇ (a' * I * x) <| by simpa).comp
    (f := fun u : ‚Ñù ‚Ü¶ (a' * I * u, I * u)) (by fun_prop)

lemma continuousOn_cosKernel (a : UnitAddCircle) : ContinuousOn (cosKernel a) (Ioi 0) := by
  induction a using QuotientAddGroup.induction_on with | H a' =>
  apply continuous_re.comp_continuousOn (f := fun x ‚Ü¶ (cosKernel a' x : ‚ÑÇ))
  simp only [cosKernel_def]
  refine continuousOn_of_forall_continuousAt (fun x hx ‚Ü¶ ?_)
  exact (continuousAt_jacobiTheta‚ÇÇ a' <| by simpa).comp
    (f := fun u : ‚Ñù ‚Ü¶ ((a' : ‚ÑÇ), I * u)) (by fun_prop)

lemma evenKernel_functional_equation (a : UnitAddCircle) (x : ‚Ñù) :
    evenKernel a x = 1 / x ^ (1 / 2 : ‚Ñù) * cosKernel a (1 / x) := by
  rcases le_or_gt x 0 with hx | hx
  ¬∑ rw [evenKernel_undef _ hx, cosKernel_undef, mul_zero]
    exact div_nonpos_of_nonneg_of_nonpos zero_le_one hx
  induction a using QuotientAddGroup.induction_on with | H a =>
  rw [‚Üê ofReal_inj, ofReal_mul, evenKernel_def, cosKernel_def, jacobiTheta‚ÇÇ_functional_equation]
  have h1 : I * ‚Üë(1 / x) = -1 / (I * x) := by
    push_cast
    rw [‚Üê div_div, mul_one_div, div_I, neg_one_mul, neg_neg]
  have hx' : I * x ‚â† 0 := mul_ne_zero I_ne_zero (ofReal_ne_zero.mpr hx.ne')
  have h2 : a * I * x / (I * x) = a := by
    rw [div_eq_iff hx']
    ring
  have h3 : 1 / (-I * (I * x)) ^ (1 / 2 : ‚ÑÇ) = 1 / ‚Üë(x ^ (1 / 2 : ‚Ñù)) := by
    rw [neg_mul, ‚Üê mul_assoc, I_mul_I, neg_one_mul, neg_neg,ofReal_cpow hx.le, ofReal_div,
      ofReal_one, ofReal_ofNat]
  have h4 : -œÄ * I * (a * I * x) ^ 2 / (I * x) = - (-œÄ * a ^ 2 * x) := by
    rw [mul_pow, mul_pow, I_sq, div_eq_iff hx']
    ring
  rw [h1, h2, h3, h4, ‚Üê mul_assoc, mul_comm (cexp _), mul_assoc _ (cexp _) (cexp _),
    ‚Üê Complex.exp_add, neg_add_cancel, Complex.exp_zero, mul_one, ofReal_div, ofReal_one]

end kernel_defs

section asymp

/-!
## Formulae for the kernels as sums
-/

lemma hasSum_int_evenKernel (a : ‚Ñù) {t : ‚Ñù} (ht : 0 < t) :
    HasSum (fun n : ‚Ñ§ ‚Ü¶ rexp (-œÄ * (n + a) ^ 2 * t)) (evenKernel a t) := by
  rw [‚Üê hasSum_ofReal, evenKernel_def]
  have (n : ‚Ñ§) : cexp (-(œÄ * (n + a) ^ 2 * t)) = cexp (-(œÄ * a ^ 2 * t)) *
      jacobiTheta‚ÇÇ_term n (a * I * t) (I * t) := by
    rw [jacobiTheta‚ÇÇ_term, ‚Üê Complex.exp_add]
    ring_nf
    simp
  simpa [this] using (hasSum_jacobiTheta‚ÇÇ_term _ (by simpa)).mul_left _

lemma hasSum_int_cosKernel (a : ‚Ñù) {t : ‚Ñù} (ht : 0 < t) :
    HasSum (fun n : ‚Ñ§ ‚Ü¶ cexp (2 * œÄ * I * a * n) * rexp (-œÄ * n ^ 2 * t)) ‚Üë(cosKernel a t) := by
  rw [cosKernel_def a t]
  have (n : ‚Ñ§) : cexp (2 * œÄ * I * a * n) * cexp (-(œÄ * n ^ 2 * t)) =
      jacobiTheta‚ÇÇ_term n a (I * ‚Üët) := by
    rw [jacobiTheta‚ÇÇ_term, ‚Üê Complex.exp_add]
    ring_nf
    simp [sub_eq_add_neg]
  simpa [this] using hasSum_jacobiTheta‚ÇÇ_term _ (by simpa)

/-- Modified version of `hasSum_int_evenKernel` omitting the constant term at `‚àû`. -/
lemma hasSum_int_evenKernel‚ÇÄ (a : ‚Ñù) {t : ‚Ñù} (ht : 0 < t) :
    HasSum (fun n : ‚Ñ§ ‚Ü¶ if n + a = 0 then 0 else rexp (-œÄ * (n + a) ^ 2 * t))
    (evenKernel a t - if (a : UnitAddCircle) = 0 then 1 else 0) := by
  haveI := Classical.propDecidable -- speed up instance search for `if / then / else`
  simp_rw [AddCircle.coe_eq_zero_iff, zsmul_one]
  split_ifs with h
  ¬∑ obtain ‚ü®k, rfl‚ü© := h
    simpa [‚Üê Int.cast_add, add_eq_zero_iff_eq_neg]
      using hasSum_ite_sub_hasSum (hasSum_int_evenKernel (k : ‚Ñù) ht) (-k)
  ¬∑ suffices ‚àÄ (n : ‚Ñ§), n + a ‚â† 0 by simpa [this] using hasSum_int_evenKernel a ht
    contrapose! h
    let ‚ü®n, hn‚ü© := h
    exact ‚ü®-n, by simpa [neg_eq_iff_add_eq_zero]‚ü©

lemma hasSum_int_cosKernel‚ÇÄ (a : ‚Ñù) {t : ‚Ñù} (ht : 0 < t) :
    HasSum (fun n : ‚Ñ§ ‚Ü¶ if n = 0 then 0 else cexp (2 * œÄ * I * a * n) * rexp (-œÄ * n ^ 2 * t))
    (‚Üë(cosKernel a t) - 1) := by
  simpa using hasSum_ite_sub_hasSum (hasSum_int_cosKernel a ht) 0

lemma hasSum_nat_cosKernel‚ÇÄ (a : ‚Ñù) {t : ‚Ñù} (ht : 0 < t) :
    HasSum (fun n : ‚Ñï ‚Ü¶ 2 * Real.cos (2 * œÄ * a * (n + 1)) * rexp (-œÄ * (n + 1) ^ 2 * t))
    (cosKernel a t - 1) := by
  rw [‚Üê hasSum_ofReal, ofReal_sub, ofReal_one]
  have := (hasSum_int_cosKernel a ht).nat_add_neg
  rw [‚Üê hasSum_nat_add_iff' 1] at this
  simp_rw [Finset.sum_range_one, Nat.cast_zero, neg_zero, Int.cast_zero, zero_pow two_ne_zero,
    mul_zero, zero_mul, Complex.exp_zero, Real.exp_zero, ofReal_one, mul_one, Int.cast_neg,
    Int.cast_natCast, neg_sq, ‚Üê add_mul, add_sub_assoc, ‚Üê sub_sub, sub_self, zero_sub,
    ‚Üê sub_eq_add_neg, mul_neg] at this
  refine this.congr_fun fun n ‚Ü¶ ?_
  push_cast
  rw [Complex.cos, mul_div_cancel‚ÇÄ _ two_ne_zero]
  congr 3 <;> ring

/-!
## Asymptotics of the kernels as `t ‚Üí ‚àû`
-/

/-- The function `evenKernel a - L` has exponential decay at `+‚àû`, where `L = 1` if
`a = 0` and `L = 0` otherwise. -/
lemma isBigO_atTop_evenKernel_sub (a : UnitAddCircle) : ‚àÉ p : ‚Ñù, 0 < p ‚àß
    (evenKernel a ¬∑ - (if a = 0 then 1 else 0)) =O[atTop] (rexp <| -p * ¬∑) := by
  induction a using QuotientAddGroup.induction_on with | H b =>
  obtain ‚ü®p, hp, hp'‚ü© := HurwitzKernelBounds.isBigO_atTop_F_int_zero_sub b
  refine ‚ü®p, hp, (EventuallyEq.isBigO ?_).trans hp'‚ü©
  filter_upwards [eventually_gt_atTop 0] with t h
  simp [‚Üê (hasSum_int_evenKernel b h).tsum_eq, HurwitzKernelBounds.F_int, HurwitzKernelBounds.f_int]

/-- The function `cosKernel a - 1` has exponential decay at `+‚àû`, for any `a`. -/
lemma isBigO_atTop_cosKernel_sub (a : UnitAddCircle) :
    ‚àÉ p, 0 < p ‚àß IsBigO atTop (cosKernel a ¬∑ - 1) (fun x ‚Ü¶ Real.exp (-p * x)) := by
  induction a using QuotientAddGroup.induction_on with | H a =>
  obtain ‚ü®p, hp, hp'‚ü© := HurwitzKernelBounds.isBigO_atTop_F_nat_zero_sub zero_le_one
  refine ‚ü®p, hp, (Eventually.isBigO ?_).trans (hp'.const_mul_left 2)‚ü©
  filter_upwards [eventually_gt_atTop 0] with t ht
  simp only [eq_false_intro one_ne_zero, if_false, sub_zero,
    ‚Üê (hasSum_nat_cosKernel‚ÇÄ a ht).tsum_eq, HurwitzKernelBounds.F_nat]
  apply tsum_of_norm_bounded ((HurwitzKernelBounds.summable_f_nat 0 1 ht).hasSum.mul_left 2)
  intro n
  rw [norm_mul, norm_mul, norm_two, mul_assoc, mul_le_mul_iff_of_pos_left two_pos,
    norm_of_nonneg (exp_pos _).le, HurwitzKernelBounds.f_nat, pow_zero, one_mul, Real.norm_eq_abs]
  exact mul_le_of_le_one_left (exp_pos _).le (abs_cos_le_one _)

end asymp

section FEPair
/-!
## Construction of a FE-pair
-/

/-- A `WeakFEPair` structure with `f = evenKernel a` and `g = cosKernel a`. -/
def hurwitzEvenFEPair (a : UnitAddCircle) : WeakFEPair ‚ÑÇ where
  f := ofReal ‚àò evenKernel a
  g := ofReal ‚àò cosKernel a
  hf_int := (continuous_ofReal.comp_continuousOn (continuousOn_evenKernel a)).locallyIntegrableOn
    measurableSet_Ioi
  hg_int := (continuous_ofReal.comp_continuousOn (continuousOn_cosKernel a)).locallyIntegrableOn
    measurableSet_Ioi
  k := 1 / 2
  hk := one_half_pos
  Œµ := 1
  hŒµ := one_ne_zero
  f‚ÇÄ := if a = 0 then 1 else 0
  hf_top r := by
    let ‚ü®v, hv, hv'‚ü© := isBigO_atTop_evenKernel_sub a
    rw [‚Üê isBigO_norm_left] at hv' ‚ä¢
    conv at hv' =>
      enter [2, x]; rw [‚Üê norm_real, ofReal_sub, apply_ite ((‚Üë) : ‚Ñù ‚Üí ‚ÑÇ), ofReal_one, ofReal_zero]
    exact hv'.trans (isLittleO_exp_neg_mul_rpow_atTop hv _).isBigO
  g‚ÇÄ := 1
  hg_top r := by
    obtain ‚ü®p, hp, hp'‚ü© := isBigO_atTop_cosKernel_sub a
    simpa using isBigO_ofReal_left.mpr <| hp'.trans (isLittleO_exp_neg_mul_rpow_atTop hp r).isBigO
  h_feq x hx := by simp [‚Üê ofReal_mul, evenKernel_functional_equation, inv_rpow (le_of_lt hx)]

@[simp]
lemma hurwitzEvenFEPair_zero_symm :
    (hurwitzEvenFEPair 0).symm = hurwitzEvenFEPair 0 := by
  unfold hurwitzEvenFEPair WeakFEPair.symm
  congr 1 <;> simp [evenKernel_eq_cosKernel_of_zero]

@[simp]
lemma hurwitzEvenFEPair_neg (a : UnitAddCircle) : hurwitzEvenFEPair (-a) = hurwitzEvenFEPair a := by
  unfold hurwitzEvenFEPair
  congr 1 <;> simp [Function.comp_def]

/-!
## Definition of the completed even Hurwitz zeta function
-/

/--
The meromorphic function of `s` which agrees with
`1 / 2 * Gamma (s / 2) * œÄ ^ (-s / 2) * ‚àë' (n : ‚Ñ§), 1 / |n + a| ^ s` for `1 < re s`.
-/
def completedHurwitzZetaEven (a : UnitAddCircle) (s : ‚ÑÇ) : ‚ÑÇ :=
  ((hurwitzEvenFEPair a).Œõ (s / 2)) / 2

/-- The entire function differing from `completedHurwitzZetaEven a s` by a linear combination of
`1 / s` and `1 / (1 - s)`. -/
def completedHurwitzZetaEven‚ÇÄ (a : UnitAddCircle) (s : ‚ÑÇ) : ‚ÑÇ :=
  ((hurwitzEvenFEPair a).Œõ‚ÇÄ (s / 2)) / 2

lemma completedHurwitzZetaEven_eq (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedHurwitzZetaEven a s =
    completedHurwitzZetaEven‚ÇÄ a s - (if a = 0 then 1 else 0) / s - 1 / (1 - s) := by
  rw [completedHurwitzZetaEven, WeakFEPair.Œõ, sub_div, sub_div]
  congr 1
  ¬∑ change completedHurwitzZetaEven‚ÇÄ a s - (1 / (s / 2)) ‚Ä¢ (if a = 0 then 1 else 0) / 2 =
      completedHurwitzZetaEven‚ÇÄ a s - (if a = 0 then 1 else 0) / s
    rw [smul_eq_mul, mul_comm, mul_div_assoc, div_div, div_mul_cancel‚ÇÄ _ two_ne_zero, mul_one_div]
  ¬∑ change (1 / (‚Üë(1 / 2 : ‚Ñù) - s / 2)) ‚Ä¢ 1 / 2 = 1 / (1 - s)
    push_cast
    rw [smul_eq_mul, mul_one, ‚Üê sub_div, div_div, div_mul_cancel‚ÇÄ _ two_ne_zero]

/--
The meromorphic function of `s` which agrees with
`Gamma (s / 2) * œÄ ^ (-s / 2) * ‚àë' n : ‚Ñï, cos (2 * œÄ * a * n) / n ^ s` for `1 < re s`.
-/
def completedCosZeta (a : UnitAddCircle) (s : ‚ÑÇ) : ‚ÑÇ :=
  ((hurwitzEvenFEPair a).symm.Œõ (s / 2)) / 2

/-- The entire function differing from `completedCosZeta a s` by a linear combination of
`1 / s` and `1 / (1 - s)`. -/
def completedCosZeta‚ÇÄ (a : UnitAddCircle) (s : ‚ÑÇ) : ‚ÑÇ :=
  ((hurwitzEvenFEPair a).symm.Œõ‚ÇÄ (s / 2)) / 2

lemma completedCosZeta_eq (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedCosZeta a s =
    completedCosZeta‚ÇÄ a s - 1 / s - (if a = 0 then 1 else 0) / (1 - s) := by
  rw [completedCosZeta, WeakFEPair.Œõ, sub_div, sub_div]
  congr 1
  ¬∑ rw [completedCosZeta‚ÇÄ, WeakFEPair.symm, hurwitzEvenFEPair, smul_eq_mul, mul_one, div_div,
      div_mul_cancel‚ÇÄ _ (two_ne_zero' ‚ÑÇ)]
  ¬∑ simp_rw [WeakFEPair.symm, hurwitzEvenFEPair, push_cast, inv_one, smul_eq_mul,
      mul_comm _ (if _ then _ else _), mul_div_assoc, div_div, ‚Üê sub_div,
      div_mul_cancel‚ÇÄ _ (two_ne_zero' ‚ÑÇ), mul_one_div]

/-!
## Parity and functional equations
-/

@[simp]
lemma completedHurwitzZetaEven_neg (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedHurwitzZetaEven (-a) s = completedHurwitzZetaEven a s := by
  simp [completedHurwitzZetaEven]

@[simp]
lemma completedHurwitzZetaEven‚ÇÄ_neg (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedHurwitzZetaEven‚ÇÄ (-a) s = completedHurwitzZetaEven‚ÇÄ a s := by
  simp [completedHurwitzZetaEven‚ÇÄ]

@[simp]
lemma completedCosZeta_neg (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedCosZeta (-a) s = completedCosZeta a s := by
  simp [completedCosZeta]

@[simp]
lemma completedCosZeta‚ÇÄ_neg (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedCosZeta‚ÇÄ (-a) s = completedCosZeta‚ÇÄ a s := by
  simp [completedCosZeta‚ÇÄ]

/-- Functional equation for the even Hurwitz zeta function. -/
lemma completedHurwitzZetaEven_one_sub (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedHurwitzZetaEven a (1 - s) = completedCosZeta a s := by
  rw [completedHurwitzZetaEven, completedCosZeta, sub_div,
    (by norm_num : (1 / 2 : ‚ÑÇ) = ‚Üë(1 / 2 : ‚Ñù)),
    (by rfl : (1 / 2 : ‚Ñù) = (hurwitzEvenFEPair a).k),
    (hurwitzEvenFEPair a).functional_equation (s / 2),
    (by rfl : (hurwitzEvenFEPair a).Œµ = 1),
    one_smul]

/-- Functional equation for the even Hurwitz zeta function with poles removed. -/
lemma completedHurwitzZetaEven‚ÇÄ_one_sub (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedHurwitzZetaEven‚ÇÄ a (1 - s) = completedCosZeta‚ÇÄ a s := by
  rw [completedHurwitzZetaEven‚ÇÄ, completedCosZeta‚ÇÄ, sub_div,
    (by norm_num : (1 / 2 : ‚ÑÇ) = ‚Üë(1 / 2 : ‚Ñù)),
    (by rfl : (1 / 2 : ‚Ñù) = (hurwitzEvenFEPair a).k),
    (hurwitzEvenFEPair a).functional_equation‚ÇÄ (s / 2),
    (by rfl : (hurwitzEvenFEPair a).Œµ = 1),
    one_smul]

/-- Functional equation for the even Hurwitz zeta function (alternative form). -/
lemma completedCosZeta_one_sub (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedCosZeta a (1 - s) = completedHurwitzZetaEven a s := by
  rw [‚Üê completedHurwitzZetaEven_one_sub, sub_sub_cancel]

/-- Functional equation for the even Hurwitz zeta function with poles removed (alternative form). -/
lemma completedCosZeta‚ÇÄ_one_sub (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedCosZeta‚ÇÄ a (1 - s) = completedHurwitzZetaEven‚ÇÄ a s := by
  rw [‚Üê completedHurwitzZetaEven‚ÇÄ_one_sub, sub_sub_cancel]

end FEPair

/-!
## Differentiability and residues
-/

section FEPair

/--
The even Hurwitz completed zeta is differentiable away from `s = 0` and `s = 1` (and also at
`s = 0` if `a ‚â† 0`)
-/
lemma differentiableAt_completedHurwitzZetaEven
    (a : UnitAddCircle) {s : ‚ÑÇ} (hs : s ‚â† 0 ‚à® a ‚â† 0) (hs' : s ‚â† 1) :
    DifferentiableAt ‚ÑÇ (completedHurwitzZetaEven a) s := by
  refine (((hurwitzEvenFEPair a).differentiableAt_Œõ ?_ (Or.inl ?_)).comp s
      (differentiableAt_id.div_const _)).div_const _
  ¬∑ rcases hs with h | h <;>
    simp [hurwitzEvenFEPair, h]
  ¬∑ change s / 2 ‚â† ‚Üë(1 / 2 : ‚Ñù)
    rw [ofReal_div, ofReal_one, ofReal_ofNat]
    exact hs' ‚àò (div_left_inj' two_ne_zero).mp

lemma differentiable_completedHurwitzZetaEven‚ÇÄ (a : UnitAddCircle) :
    Differentiable ‚ÑÇ (completedHurwitzZetaEven‚ÇÄ a) :=
  ((hurwitzEvenFEPair a).differentiable_Œõ‚ÇÄ.comp (differentiable_id.div_const _)).div_const _

/-- The difference of two completed even Hurwitz zeta functions is differentiable at `s = 1`. -/
lemma differentiableAt_one_completedHurwitzZetaEven_sub_completedHurwitzZetaEven
    (a b : UnitAddCircle) :
    DifferentiableAt ‚ÑÇ (fun s ‚Ü¶ completedHurwitzZetaEven a s - completedHurwitzZetaEven b s) 1 := by
  have (s) : completedHurwitzZetaEven a s - completedHurwitzZetaEven b s =
      completedHurwitzZetaEven‚ÇÄ a s - completedHurwitzZetaEven‚ÇÄ b s -
      ((if a = 0 then 1 else 0) - (if b = 0 then 1 else 0)) / s := by
    simp_rw [completedHurwitzZetaEven_eq, sub_div]
    abel
  rw [funext this]
  refine .sub ?_ <| (differentiable_const _ _).div (differentiable_id _) one_ne_zero
  apply DifferentiableAt.sub <;> apply differentiable_completedHurwitzZetaEven‚ÇÄ

lemma differentiableAt_completedCosZeta
    (a : UnitAddCircle) {s : ‚ÑÇ} (hs : s ‚â† 0) (hs' : s ‚â† 1 ‚à® a ‚â† 0) :
    DifferentiableAt ‚ÑÇ (completedCosZeta a) s := by
  refine (((hurwitzEvenFEPair a).symm.differentiableAt_Œõ (Or.inl ?_) ?_).comp s
      (differentiableAt_id.div_const _)).div_const _
  ¬∑ exact div_ne_zero_iff.mpr ‚ü®hs, two_ne_zero‚ü©
  ¬∑ change s / 2 ‚â† ‚Üë(1 / 2 : ‚Ñù) ‚à® (if a = 0 then 1 else 0) = 0
    refine Or.imp (fun h ‚Ü¶ ?_) (fun ha ‚Ü¶ ?_) hs'
    ¬∑ simpa [push_cast] using h ‚àò (div_left_inj' two_ne_zero).mp
    ¬∑ simpa

lemma differentiable_completedCosZeta‚ÇÄ (a : UnitAddCircle) :
    Differentiable ‚ÑÇ (completedCosZeta‚ÇÄ a) :=
  ((hurwitzEvenFEPair a).symm.differentiable_Œõ‚ÇÄ.comp (differentiable_id.div_const _)).div_const _

private lemma tendsto_div_two_punctured_nhds (a : ‚ÑÇ) :
    Tendsto (fun s : ‚ÑÇ ‚Ü¶ s / 2) (ùìù[‚â†] a) (ùìù[‚â†] (a / 2)) :=
  le_of_eq ((Homeomorph.mulRight‚ÇÄ _ (inv_ne_zero (two_ne_zero' ‚ÑÇ))).map_punctured_nhds_eq a)

/-- The residue of `completedHurwitzZetaEven a s` at `s = 1` is equal to `1`. -/
lemma completedHurwitzZetaEven_residue_one (a : UnitAddCircle) :
    Tendsto (fun s ‚Ü¶ (s - 1) * completedHurwitzZetaEven a s) (ùìù[‚â†] 1) (ùìù 1) := by
  have h1 : Tendsto (fun s : ‚ÑÇ ‚Ü¶ (s - ‚Üë(1  / 2 : ‚Ñù)) * _) (ùìù[‚â†] ‚Üë(1  / 2 : ‚Ñù))
    (ùìù ((1 : ‚ÑÇ) * (1 : ‚ÑÇ))) := (hurwitzEvenFEPair a).Œõ_residue_k
  simp only [push_cast, one_mul] at h1
  refine (h1.comp <| tendsto_div_two_punctured_nhds 1).congr (fun s ‚Ü¶ ?_)
  rw [completedHurwitzZetaEven, Function.comp_apply, ‚Üê sub_div, div_mul_eq_mul_div, mul_div_assoc]

/-- The residue of `completedHurwitzZetaEven a s` at `s = 0` is equal to `-1` if `a = 0`, and `0`
otherwise. -/
lemma completedHurwitzZetaEven_residue_zero (a : UnitAddCircle) :
    Tendsto (fun s ‚Ü¶ s * completedHurwitzZetaEven a s) (ùìù[‚â†] 0) (ùìù (if a = 0 then -1 else 0)) := by
  have h1 : Tendsto (fun s : ‚ÑÇ ‚Ü¶ s * _) (ùìù[‚â†] 0)
    (ùìù (-(if a = 0 then 1 else 0))) := (hurwitzEvenFEPair a).Œõ_residue_zero
  have : -(if a = 0 then (1 : ‚ÑÇ) else 0) = (if a = 0 then -1 else 0) := by { split_ifs <;> simp }
  simp only [this, push_cast, one_mul] at h1
  refine (h1.comp <| zero_div (2 : ‚ÑÇ) ‚ñ∏ (tendsto_div_two_punctured_nhds 0)).congr (fun s ‚Ü¶ ?_)
  simp [completedHurwitzZetaEven, div_mul_eq_mul_div, mul_div_assoc]

lemma completedCosZeta_residue_zero (a : UnitAddCircle) :
    Tendsto (fun s ‚Ü¶ s * completedCosZeta a s) (ùìù[‚â†] 0) (ùìù (-1)) := by
  have h1 : Tendsto (fun s : ‚ÑÇ ‚Ü¶ s * _) (ùìù[‚â†] 0)
    (ùìù (-1)) := (hurwitzEvenFEPair a).symm.Œõ_residue_zero
  refine (h1.comp <| zero_div (2 : ‚ÑÇ) ‚ñ∏ (tendsto_div_two_punctured_nhds 0)).congr (fun s ‚Ü¶ ?_)
  simp [completedCosZeta, div_mul_eq_mul_div, mul_div_assoc]

end FEPair

/-!
## Relation to the Dirichlet series for `1 < re s`
-/

/-- Formula for `completedCosZeta` as a Dirichlet series in the convergence range
(first version, with sum over `‚Ñ§`). -/
lemma hasSum_int_completedCosZeta (a : ‚Ñù) {s : ‚ÑÇ} (hs : 1 < re s) :
    HasSum (fun n : ‚Ñ§ ‚Ü¶ Gamma‚Ñù s * cexp (2 * œÄ * I * a * n) / (‚Üë|n| : ‚ÑÇ) ^ s / 2)
    (completedCosZeta a s) := by
  let c (n : ‚Ñ§) : ‚ÑÇ := cexp (2 * œÄ * I * a * n) / 2
  have hF t (ht : 0 < t) : HasSum (fun n : ‚Ñ§ ‚Ü¶ if n = 0 then 0 else c n * rexp (-œÄ * n ^ 2 * t))
      ((cosKernel a t - 1) / 2) := by
    refine ((hasSum_int_cosKernel‚ÇÄ a ht).div_const 2).congr_fun fun n ‚Ü¶ ?_
    split_ifs <;> simp [c, div_mul_eq_mul_div]
  simp only [‚Üê Int.cast_eq_zero (Œ± := ‚Ñù)] at hF
  rw [show completedCosZeta a s = mellin (fun t ‚Ü¶ (cosKernel a t - 1 : ‚ÑÇ) / 2) (s / 2) by
    rw [mellin_div_const, completedCosZeta]
    congr 1
    refine ((hurwitzEvenFEPair a).symm.hasMellin (?_ : 1 / 2 < (s / 2).re)).2.symm
    rwa [div_ofNat_re, div_lt_div_iff_of_pos_right two_pos]]
  refine (hasSum_mellin_pi_mul_sq (zero_lt_one.trans hs) hF ?_).congr_fun fun n ‚Ü¶ ?_
  ¬∑ apply (((summable_one_div_int_add_rpow 0 s.re).mpr hs).div_const 2).of_norm_bounded
    intro i
    simp only [c, (by { push_cast; ring } : 2 * œÄ * I * a * i = ‚Üë(2 * œÄ * a * i) * I), norm_div,
      RCLike.norm_ofNat, norm_norm, Complex.norm_exp_ofReal_mul_I, add_zero, norm_one,
      norm_of_nonneg (by positivity : 0 ‚â§ |(i : ‚Ñù)| ^ s.re), div_right_comm, le_rfl]
  ¬∑ simp [c, ‚Üê Int.cast_abs, div_right_comm, mul_div_assoc]

/-- Formula for `completedCosZeta` as a Dirichlet series in the convergence range
(second version, with sum over `‚Ñï`). -/
lemma hasSum_nat_completedCosZeta (a : ‚Ñù) {s : ‚ÑÇ} (hs : 1 < re s) :
    HasSum (fun n : ‚Ñï ‚Ü¶ if n = 0 then 0 else Gamma‚Ñù s * Real.cos (2 * œÄ * a * n) / (n : ‚ÑÇ) ^ s)
    (completedCosZeta a s) := by
  have aux : ((|0| : ‚Ñ§) : ‚ÑÇ) ^ s = 0 := by
    rw [abs_zero, Int.cast_zero, zero_cpow (ne_zero_of_one_lt_re hs)]
  have hint := (hasSum_int_completedCosZeta a hs).nat_add_neg
  rw [aux, div_zero, zero_div, add_zero] at hint
  refine hint.congr_fun fun n ‚Ü¶ ?_
  split_ifs with h
  ¬∑ simp only [h, Nat.cast_zero, aux, div_zero, zero_div, neg_zero, zero_add]
  ¬∑ simp only [ofReal_cos, ofReal_mul, ofReal_ofNat, ofReal_natCast, Complex.cos,
      show 2 * œÄ * a * n * I = 2 * œÄ * I * a * n by ring, neg_mul, mul_div_assoc,
      div_right_comm _ (2 : ‚ÑÇ), Int.cast_natCast, Nat.abs_cast, Int.cast_neg, mul_neg, abs_neg, ‚Üê
      mul_add, ‚Üê add_div]

/-- Formula for `completedHurwitzZetaEven` as a Dirichlet series in the convergence range. -/
lemma hasSum_int_completedHurwitzZetaEven (a : ‚Ñù) {s : ‚ÑÇ} (hs : 1 < re s) :
    HasSum (fun n : ‚Ñ§ ‚Ü¶ Gamma‚Ñù s / (‚Üë|n + a| : ‚ÑÇ) ^ s / 2) (completedHurwitzZetaEven a s) := by
  have hF (t : ‚Ñù) (ht : 0 < t) : HasSum (fun n : ‚Ñ§ ‚Ü¶ if n + a = 0 then 0
      else (1 / 2 : ‚ÑÇ) * rexp (-œÄ * (n + a) ^ 2 * t))
      ((evenKernel a t - (if (a : UnitAddCircle) = 0 then 1 else 0 : ‚Ñù)) / 2) := by
    refine (ofReal_sub .. ‚ñ∏ (hasSum_ofReal.mpr (hasSum_int_evenKernel‚ÇÄ a ht)).div_const
      2).congr_fun fun n ‚Ü¶ ?_
    split_ifs
    ¬∑ rw [ofReal_zero, zero_div]
    ¬∑ rw [mul_comm, mul_one_div]
  rw [show completedHurwitzZetaEven a s = mellin (fun t ‚Ü¶ ((evenKernel (‚Üëa) t : ‚ÑÇ) -
        ‚Üë(if (a : UnitAddCircle) = 0 then 1 else 0 : ‚Ñù)) / 2) (s / 2) by
    simp_rw [mellin_div_const, apply_ite ofReal, ofReal_one, ofReal_zero]
    refine congr_arg (¬∑ / 2) ((hurwitzEvenFEPair a).hasMellin (?_ : 1 / 2 < (s / 2).re)).2.symm
    rwa [div_ofNat_re, div_lt_div_iff_of_pos_right two_pos]]
  refine (hasSum_mellin_pi_mul_sq (zero_lt_one.trans hs) hF ?_).congr_fun fun n ‚Ü¶ ?_
  ¬∑ simp_rw [‚Üê mul_one_div ‚Äñ_‚Äñ]
    apply Summable.mul_left
    rwa [summable_one_div_int_add_rpow]
  ¬∑ rw [mul_one_div, div_right_comm]

/-!
## The un-completed even Hurwitz zeta
-/

/-- Technical lemma which will give us differentiability of Hurwitz zeta at `s = 0`. -/
lemma differentiableAt_update_of_residue
    {Œõ : ‚ÑÇ ‚Üí ‚ÑÇ} (hf : ‚àÄ (s : ‚ÑÇ) (_ : s ‚â† 0) (_ : s ‚â† 1), DifferentiableAt ‚ÑÇ Œõ s)
    {L : ‚ÑÇ} (h_lim : Tendsto (fun s ‚Ü¶ s * Œõ s) (ùìù[‚â†] 0) (ùìù L)) (s : ‚ÑÇ) (hs' : s ‚â† 1) :
    DifferentiableAt ‚ÑÇ (Function.update (fun s ‚Ü¶ Œõ s / Gamma‚Ñù s) 0 (L / 2)) s := by
  have claim (t) (ht : t ‚â† 0) (ht' : t ‚â† 1) : DifferentiableAt ‚ÑÇ (fun u : ‚ÑÇ ‚Ü¶ Œõ u / Gamma‚Ñù u) t :=
    (hf t ht ht').mul differentiable_Gamma‚Ñù_inv.differentiableAt
  have claim2 : Tendsto (fun s : ‚ÑÇ ‚Ü¶ Œõ s / Gamma‚Ñù s) (ùìù[‚â†] 0) (ùìù <| L / 2) := by
    refine Tendsto.congr' ?_ (h_lim.div Gamma‚Ñù_residue_zero two_ne_zero)
    filter_upwards [self_mem_nhdsWithin] with s (hs : s ‚â† 0)
    rw [Pi.div_apply, ‚Üê div_div, mul_div_cancel_left‚ÇÄ _ hs]
  rcases ne_or_eq s 0 with hs | rfl
  ¬∑ -- Easy case : `s ‚â† 0`
    refine (claim s hs hs').congr_of_eventuallyEq ?_
    filter_upwards [isOpen_compl_singleton.mem_nhds hs] with x hx
    simp [Function.update_of_ne hx]
  ¬∑ -- Hard case : `s = 0`
    simp_rw [‚Üê claim2.limUnder_eq]
    have S_nhds : {(1 : ‚ÑÇ)}·∂ú ‚àà ùìù (0 : ‚ÑÇ) := isOpen_compl_singleton.mem_nhds hs'
    refine ((Complex.differentiableOn_update_limUnder_of_isLittleO S_nhds
      (fun t ht ‚Ü¶ (claim t ht.2 ht.1).differentiableWithinAt) ?_) 0 hs').differentiableAt S_nhds
    simp only [Gamma‚Ñù, zero_div, div_zero, Complex.Gamma_zero, mul_zero, cpow_zero, sub_zero]
    -- Remains to show completed zeta is `o (s ^ (-1))` near 0.
    refine (isBigO_const_of_tendsto claim2 <| one_ne_zero' ‚ÑÇ).trans_isLittleO ?_
    rw [isLittleO_iff_tendsto']
    ¬∑ exact Tendsto.congr (fun x ‚Ü¶ by rw [‚Üê one_div, one_div_one_div]) nhdsWithin_le_nhds
    ¬∑ exact eventually_of_mem self_mem_nhdsWithin fun x hx hx' ‚Ü¶ (hx <| inv_eq_zero.mp hx').elim

/-- The even part of the Hurwitz zeta function, i.e. the meromorphic function of `s` which agrees
with `1 / 2 * ‚àë' (n : ‚Ñ§), 1 / |n + a| ^ s` for `1 < re s` -/
noncomputable def hurwitzZetaEven (a : UnitAddCircle) :=
  Function.update (fun s ‚Ü¶ completedHurwitzZetaEven a s / Gamma‚Ñù s)
  0 (if a = 0 then -1 / 2 else 0)

lemma hurwitzZetaEven_def_of_ne_or_ne {a : UnitAddCircle} {s : ‚ÑÇ} (h : a ‚â† 0 ‚à® s ‚â† 0) :
    hurwitzZetaEven a s = completedHurwitzZetaEven a s / Gamma‚Ñù s := by
  rw [hurwitzZetaEven]
  rcases ne_or_eq s 0 with h' | rfl
  ¬∑ rw [Function.update_of_ne h']
  ¬∑ simpa [Gamma‚Ñù] using h

lemma hurwitzZetaEven_apply_zero (a : UnitAddCircle) :
    hurwitzZetaEven a 0 = if a = 0 then -1 / 2 else 0 :=
  Function.update_self ..

lemma hurwitzZetaEven_neg (a : UnitAddCircle) (s : ‚ÑÇ) :
    hurwitzZetaEven (-a) s = hurwitzZetaEven a s := by
  simp [hurwitzZetaEven]

/-- The trivial zeroes of the even Hurwitz zeta function. -/
theorem hurwitzZetaEven_neg_two_mul_nat_add_one (a : UnitAddCircle) (n : ‚Ñï) :
    hurwitzZetaEven a (-2 * (n + 1)) = 0 := by
  have : (-2 : ‚ÑÇ) * (n + 1) ‚â† 0 :=
    mul_ne_zero (neg_ne_zero.mpr two_ne_zero) (Nat.cast_add_one_ne_zero n)
  rw [hurwitzZetaEven, Function.update_of_ne this, Gamma‚Ñù_eq_zero_iff.mpr ‚ü®n + 1, by simp‚ü©,
    div_zero]

/-- The Hurwitz zeta function is differentiable everywhere except at `s = 1`. This is true
even in the delicate case `a = 0` and `s = 0` (where the completed zeta has a pole, but this is
cancelled out by the Gamma factor). -/
lemma differentiableAt_hurwitzZetaEven (a : UnitAddCircle) {s : ‚ÑÇ} (hs' : s ‚â† 1) :
    DifferentiableAt ‚ÑÇ (hurwitzZetaEven a) s := by
  have := differentiableAt_update_of_residue
    (fun t ht ht' ‚Ü¶ differentiableAt_completedHurwitzZetaEven a (Or.inl ht) ht')
    (completedHurwitzZetaEven_residue_zero a) s hs'
  simp_rw [div_eq_mul_inv, ite_mul, zero_mul, ‚Üê div_eq_mul_inv] at this
  exact this

lemma hurwitzZetaEven_residue_one (a : UnitAddCircle) :
    Tendsto (fun s ‚Ü¶ (s - 1) * hurwitzZetaEven a s) (ùìù[‚â†] 1) (ùìù 1) := by
  have : Tendsto (fun s ‚Ü¶ (s - 1) * completedHurwitzZetaEven a s / Gamma‚Ñù s) (ùìù[‚â†] 1) (ùìù 1) := by
    simpa only [Gamma‚Ñù_one, inv_one, mul_one] using (completedHurwitzZetaEven_residue_one a).mul
      <| (differentiable_Gamma‚Ñù_inv.continuous.tendsto _).mono_left nhdsWithin_le_nhds
  refine this.congr' ?_
  filter_upwards [eventually_ne_nhdsWithin one_ne_zero] with s hs
  simp [hurwitzZetaEven_def_of_ne_or_ne (Or.inr hs), mul_div_assoc]

lemma differentiableAt_hurwitzZetaEven_sub_one_div (a : UnitAddCircle) :
    DifferentiableAt ‚ÑÇ (fun s ‚Ü¶ hurwitzZetaEven a s - 1 / (s - 1) / Gamma‚Ñù s) 1 := by
  suffices DifferentiableAt ‚ÑÇ
      (fun s ‚Ü¶ completedHurwitzZetaEven a s / Gamma‚Ñù s - 1 / (s - 1) / Gamma‚Ñù s) 1 by
    apply this.congr_of_eventuallyEq
    filter_upwards [eventually_ne_nhds one_ne_zero] with x hx
    rw [hurwitzZetaEven, Function.update_of_ne hx]
  simp_rw [‚Üê sub_div, div_eq_mul_inv _ (Gamma‚Ñù _)]
  refine DifferentiableAt.mul ?_ differentiable_Gamma‚Ñù_inv.differentiableAt
  simp_rw [completedHurwitzZetaEven_eq, sub_sub, add_assoc]
  conv => enter [2, s, 2]; rw [‚Üê neg_sub, div_neg, neg_add_cancel, add_zero]
  exact (differentiable_completedHurwitzZetaEven‚ÇÄ a _).sub
    <| (differentiableAt_const _).div differentiableAt_id one_ne_zero

/-- Expression for `hurwitzZetaEven a 1` as a limit. (Mathematically `hurwitzZetaEven a 1` is
undefined, but our construction assigns some value to it; this lemma is mostly of interest for
determining what that value is). -/
lemma tendsto_hurwitzZetaEven_sub_one_div_nhds_one (a : UnitAddCircle) :
    Tendsto (fun s ‚Ü¶ hurwitzZetaEven a s - 1 / (s - 1) / Gamma‚Ñù s) (ùìù 1)
    (ùìù (hurwitzZetaEven a 1)) := by
  simpa using (differentiableAt_hurwitzZetaEven_sub_one_div a).continuousAt.tendsto

lemma differentiable_hurwitzZetaEven_sub_hurwitzZetaEven (a b : UnitAddCircle) :
    Differentiable ‚ÑÇ (fun s ‚Ü¶ hurwitzZetaEven a s - hurwitzZetaEven b s) := by
  intro z
  rcases ne_or_eq z 1 with hz | rfl
  ¬∑ exact (differentiableAt_hurwitzZetaEven a hz).sub (differentiableAt_hurwitzZetaEven b hz)
  ¬∑ convert (differentiableAt_hurwitzZetaEven_sub_one_div a).fun_sub
      (differentiableAt_hurwitzZetaEven_sub_one_div b) using 2 with s
    abel

/--
Formula for `hurwitzZetaEven` as a Dirichlet series in the convergence range, with sum over `‚Ñ§`.
-/
lemma hasSum_int_hurwitzZetaEven (a : ‚Ñù) {s : ‚ÑÇ} (hs : 1 < re s) :
    HasSum (fun n : ‚Ñ§ ‚Ü¶ 1 / (‚Üë|n + a| : ‚ÑÇ) ^ s / 2) (hurwitzZetaEven a s) := by
  rw [hurwitzZetaEven, Function.update_of_ne (ne_zero_of_one_lt_re hs)]
  have := (hasSum_int_completedHurwitzZetaEven a hs).div_const (Gamma‚Ñù s)
  exact this.congr_fun fun n ‚Ü¶ by simp only [div_right_comm _ _ (Gamma‚Ñù _),
    div_self (Gamma‚Ñù_ne_zero_of_re_pos (zero_lt_one.trans hs))]

/-- Formula for `hurwitzZetaEven` as a Dirichlet series in the convergence range, with sum over `‚Ñï`
(version with absolute values) -/
lemma hasSum_nat_hurwitzZetaEven (a : ‚Ñù) {s : ‚ÑÇ} (hs : 1 < re s) :
    HasSum (fun n : ‚Ñï ‚Ü¶ (1 / (‚Üë|n + a| : ‚ÑÇ) ^ s + 1 / (‚Üë|n + 1 - a| : ‚ÑÇ) ^ s) / 2)
    (hurwitzZetaEven a s) := by
  refine (hasSum_int_hurwitzZetaEven a hs).nat_add_neg_add_one.congr_fun fun n ‚Ü¶ ?_
  simp [‚Üê abs_neg (n + 1 - a), -neg_sub, neg_sub', add_div]

/-- Formula for `hurwitzZetaEven` as a Dirichlet series in the convergence range, with sum over `‚Ñï`
(version without absolute values, assuming `a ‚àà Icc 0 1`) -/
lemma hasSum_nat_hurwitzZetaEven_of_mem_Icc {a : ‚Ñù} (ha : a ‚àà Icc 0 1) {s : ‚ÑÇ} (hs : 1 < re s) :
    HasSum (fun n : ‚Ñï ‚Ü¶ (1 / (n + a : ‚ÑÇ) ^ s + 1 / (n + 1 - a : ‚ÑÇ) ^ s) / 2)
    (hurwitzZetaEven a s) := by
  refine (hasSum_nat_hurwitzZetaEven a hs).congr_fun fun n ‚Ü¶ ?_
  congr 2 <;>
  rw [abs_of_nonneg (by linarith [ha.1, ha.2])] <;>
  simp

/-!
## The un-completed cosine zeta
-/

/-- The cosine zeta function, i.e. the meromorphic function of `s` which agrees
with `‚àë' (n : ‚Ñï), cos (2 * œÄ * a * n) / n ^ s` for `1 < re s`. -/
noncomputable def cosZeta (a : UnitAddCircle) :=
  Function.update (fun s : ‚ÑÇ ‚Ü¶ completedCosZeta a s / Gamma‚Ñù s) 0 (-1 / 2)

lemma cosZeta_apply_zero (a : UnitAddCircle) : cosZeta a 0 = -1 / 2 :=
  Function.update_self ..

lemma cosZeta_neg (a : UnitAddCircle) (s : ‚ÑÇ) :
    cosZeta (-a) s = cosZeta a s := by
  simp [cosZeta]

/-- The trivial zeroes of the cosine zeta function. -/
theorem cosZeta_neg_two_mul_nat_add_one (a : UnitAddCircle) (n : ‚Ñï) :
    cosZeta a (-2 * (n + 1)) = 0 := by
  have : (-2 : ‚ÑÇ) * (n + 1) ‚â† 0 :=
    mul_ne_zero (neg_ne_zero.mpr two_ne_zero) (Nat.cast_add_one_ne_zero n)
  rw [cosZeta, Function.update_of_ne this,
    Gamma‚Ñù_eq_zero_iff.mpr ‚ü®n + 1, by rw [neg_mul, Nat.cast_add_one]‚ü©, div_zero]

/-- The cosine zeta function is differentiable everywhere, except at `s = 1` if `a = 0`. -/
lemma differentiableAt_cosZeta (a : UnitAddCircle) {s : ‚ÑÇ} (hs' : s ‚â† 1 ‚à® a ‚â† 0) :
    DifferentiableAt ‚ÑÇ (cosZeta a) s := by
  rcases ne_or_eq s 1 with hs' | rfl
  ¬∑ exact differentiableAt_update_of_residue (fun _ ht ht' ‚Ü¶
      differentiableAt_completedCosZeta a ht (Or.inl ht')) (completedCosZeta_residue_zero a) s hs'
  ¬∑ apply ((differentiableAt_completedCosZeta a one_ne_zero hs').mul
      (differentiable_Gamma‚Ñù_inv.differentiableAt)).congr_of_eventuallyEq
    filter_upwards [isOpen_compl_singleton.mem_nhds one_ne_zero] with x hx
    rw [cosZeta, Function.update_of_ne hx, div_eq_mul_inv]

/-- If `a ‚â† 0` then the cosine zeta function is entire. -/
lemma differentiable_cosZeta_of_ne_zero {a : UnitAddCircle} (ha : a ‚â† 0) :
    Differentiable ‚ÑÇ (cosZeta a) :=
  fun _ ‚Ü¶ differentiableAt_cosZeta a (Or.inr ha)

/-- Formula for `cosZeta` as a Dirichlet series in the convergence range, with sum over `‚Ñ§`. -/
lemma hasSum_int_cosZeta (a : ‚Ñù) {s : ‚ÑÇ} (hs : 1 < re s) :
    HasSum (fun n : ‚Ñ§ ‚Ü¶ cexp (2 * œÄ * I * a * n) / ‚Üë|n| ^ s / 2) (cosZeta a s) := by
  rw [cosZeta, Function.update_of_ne (ne_zero_of_one_lt_re hs)]
  refine ((hasSum_int_completedCosZeta a hs).div_const (Gamma‚Ñù s)).congr_fun fun n ‚Ü¶ ?_
  rw [mul_div_assoc _ (cexp _), div_right_comm _ (2 : ‚ÑÇ),
    mul_div_cancel_left‚ÇÄ _ (Gamma‚Ñù_ne_zero_of_re_pos (zero_lt_one.trans hs))]

/-- Formula for `cosZeta` as a Dirichlet series in the convergence range, with sum over `‚Ñï`. -/
lemma hasSum_nat_cosZeta (a : ‚Ñù) {s : ‚ÑÇ} (hs : 1 < re s) :
    HasSum (fun n : ‚Ñï ‚Ü¶ Real.cos (2 * œÄ * a * n) / (n : ‚ÑÇ) ^ s) (cosZeta a s) := by
  have := (hasSum_int_cosZeta a hs).nat_add_neg
  simp_rw [abs_neg, Int.cast_neg, Nat.abs_cast, Int.cast_natCast, mul_neg, abs_zero, Int.cast_zero,
    zero_cpow (ne_zero_of_one_lt_re hs), div_zero, zero_div, add_zero, ‚Üê add_div,
    div_right_comm _ _ (2 : ‚ÑÇ)] at this
  simp_rw [push_cast, Complex.cos, neg_mul]
  exact this.congr_fun fun n ‚Ü¶ by rw [show 2 * œÄ * a * n * I = 2 * œÄ * I * a * n by ring]

/-- Reformulation of `hasSum_nat_cosZeta` using `LSeriesHasSum`. -/
lemma LSeriesHasSum_cos (a : ‚Ñù) {s : ‚ÑÇ} (hs : 1 < re s) :
    LSeriesHasSum (Real.cos <| 2 * œÄ * a * ¬∑) s (cosZeta a s) :=
  (hasSum_nat_cosZeta a hs).congr_fun (LSeries.term_of_ne_zero' (ne_zero_of_one_lt_re hs) _)

/-!
## Functional equations for the un-completed zetas
-/

/-- If `s` is not in `-‚Ñï`, and either `a ‚â† 0` or `s ‚â† 1`, then
`hurwitzZetaEven a (1 - s)` is an explicit multiple of `cosZeta s`. -/
lemma hurwitzZetaEven_one_sub (a : UnitAddCircle) {s : ‚ÑÇ}
    (hs : ‚àÄ (n : ‚Ñï), s ‚â† -n) (hs' : a ‚â† 0 ‚à® s ‚â† 1) :
    hurwitzZetaEven a (1 - s) = 2 * (2 * œÄ) ^ (-s) * Gamma s * cos (œÄ * s / 2) * cosZeta a s := by
  have : hurwitzZetaEven a (1 - s) = completedHurwitzZetaEven a (1 - s) * (Gamma‚Ñù (1 - s))‚Åª¬π := by
    rw [hurwitzZetaEven_def_of_ne_or_ne, div_eq_mul_inv]
    simpa [sub_eq_zero, eq_comm (a := s)] using hs'
  rw [this, completedHurwitzZetaEven_one_sub, inv_Gamma‚Ñù_one_sub hs, cosZeta,
    Function.update_of_ne (by simpa using hs 0), ‚Üê Gamma‚ÑÇ]
  generalize Gamma‚ÑÇ s * cos (œÄ * s / 2) = A -- speeds up ring_nf call
  ring_nf

/-- If `s` is not of the form `1 - n` for `n ‚àà ‚Ñï`, then `cosZeta a (1 - s)` is an explicit
multiple of `hurwitzZetaEven s`. -/
lemma cosZeta_one_sub (a : UnitAddCircle) {s : ‚ÑÇ} (hs : ‚àÄ (n : ‚Ñï), s ‚â† 1 - n) :
    cosZeta a (1 - s) = 2 * (2 * œÄ) ^ (-s) * Gamma s * cos (œÄ * s / 2) * hurwitzZetaEven a s := by
  rw [‚Üê Gamma‚ÑÇ]
  have : cosZeta a (1 - s) = completedCosZeta a (1 - s) * (Gamma‚Ñù (1 - s))‚Åª¬π := by
    rw [cosZeta, Function.update_of_ne, div_eq_mul_inv]
    simpa [sub_eq_zero] using (hs 0).symm
  rw [this, completedCosZeta_one_sub, inv_Gamma‚Ñù_one_sub (fun n ‚Ü¶ by simpa using hs (n + 1)),
    hurwitzZetaEven_def_of_ne_or_ne (Or.inr (by simpa using hs 1))]
  generalize Gamma‚ÑÇ s * cos (œÄ * s / 2) = A -- speeds up ring_nf call
  ring_nf

end HurwitzZeta
