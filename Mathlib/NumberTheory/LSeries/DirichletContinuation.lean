/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler, Michael Stoll
-/
import Mathlib.NumberTheory.LSeries.ZMod
import Mathlib.NumberTheory.DirichletCharacter.Basic
import Mathlib.NumberTheory.EulerProduct.DirichletLSeries

/-!
# Analytic continuation of Dirichlet L-functions

We show that if `Ï‡` is a Dirichlet character `ZMod N â†’ â„‚`, for a positive integer `N`, then the
L-series of `Ï‡` has analytic continuation (away from a pole at `s = 1` if `Ï‡` is trivial), and
similarly for completed L-functions.

All definitions and theorems are in the `DirichletCharacter` namespace.

## Main definitions

* `LFunction Ï‡ s`: the L-function, defined as a linear combination of Hurwitz zeta functions.
* `completedLFunction Ï‡ s`: the completed L-function, which for *almost* all `s` is equal to
  `LFunction Ï‡ s * gammaFactor Ï‡ s` where `gammaFactor Ï‡ s` is the archimedean Gamma-factor.
* `rootNumber`: the global root number of the L-series of `Ï‡` (for `Ï‡` primitive; junk otherwise).

## Main theorems

* `LFunction_eq_LSeries`: if `1 < re s` then the `LFunction` coincides with the naive `LSeries`.
* `differentiable_LFunction`: if `Ï‡` is nontrivial then `LFunction Ï‡ s` is differentiable
  everywhere.
* `LFunction_eq_completed_div_gammaFactor`: we have
  `LFunction Ï‡ s = completedLFunction Ï‡ s / gammaFactor Ï‡ s`, unless `s = 0` and `Ï‡` is the trivial
  character modulo 1.
* `differentiable_completedLFunction`: if `Ï‡` is nontrivial then `completedLFunction Ï‡ s` is
  differentiable everywhere.
* `IsPrimitive.completedLFunction_one_sub`: the **functional equation** for Dirichlet L-functions,
  showing that if `Ï‡` is primitive modulo `N`, then
  `completedLFunction Ï‡ s = N ^ (s - 1 / 2) * rootNumber Ï‡ * completedLFunction Ï‡â»Â¹ s`.
-/

open HurwitzZeta Complex Finset ZMod Filter

open scoped Real Topology

namespace DirichletCharacter

variable {N : â„•} [NeZero N]

/--
The unique meromorphic function `â„‚ â†’ â„‚` which agrees with `âˆ‘' n : â„•, Ï‡ n / n ^ s` wherever the
latter is convergent. This is constructed as a linear combination of Hurwitz zeta functions.

Note that this is not the same as `LSeries Ï‡`: they agree in the convergence range, but
`LSeries Ï‡ s` is defined to be `0` if `re s â‰¤ 1`.
-/
@[pp_nodot]
noncomputable def LFunction (Ï‡ : DirichletCharacter â„‚ N) (s : â„‚) : â„‚ := ZMod.LFunction Ï‡ s

/--
The L-function of the (unique) Dirichlet character mod 1 is the Riemann zeta function.
(Compare `DirichletCharacter.LSeries_modOne_eq`.)
-/
@[simp] lemma LFunction_modOne_eq {Ï‡ : DirichletCharacter â„‚ 1} :
    LFunction Ï‡ = riemannZeta := by
  ext; rw [LFunction, ZMod.LFunction_modOne_eq, (by rfl : (0 : ZMod 1) = 1), map_one, one_mul]

/--
For `1 < re s` the L-function of a Dirichlet character agrees with the sum of the naive Dirichlet
series.
-/
lemma LFunction_eq_LSeries (Ï‡ : DirichletCharacter â„‚ N) {s : â„‚} (hs : 1 < re s) :
    LFunction Ï‡ s = LSeries (Ï‡ Â·) s :=
  ZMod.LFunction_eq_LSeries Ï‡ hs

lemma deriv_LFunction_eq_deriv_LSeries (Ï‡ : DirichletCharacter â„‚ N) {s : â„‚} (hs : 1 < s.re) :
    deriv (LFunction Ï‡) s = deriv (LSeries (Ï‡ Â·)) s := by
  refine Filter.EventuallyEq.deriv_eq ?_
  have h : {z | 1 < z.re} âˆˆ nhds s :=
    (isOpen_lt continuous_const continuous_re).mem_nhds hs
  filter_upwards [h] with z hz
  exact LFunction_eq_LSeries Ï‡ hz

/--
The L-function of a Dirichlet character is differentiable, except at `s = 1` if the character is
trivial.
-/
@[fun_prop]
lemma differentiableAt_LFunction (Ï‡ : DirichletCharacter â„‚ N) (s : â„‚) (hs : s â‰  1 âˆ¨ Ï‡ â‰  1) :
    DifferentiableAt â„‚ (LFunction Ï‡) s :=
  ZMod.differentiableAt_LFunction Ï‡ s (hs.imp_right Ï‡.sum_eq_zero_of_ne_one)

/-- The L-function of a non-trivial Dirichlet character is differentiable everywhere. -/
@[fun_prop]
lemma differentiable_LFunction {Ï‡ : DirichletCharacter â„‚ N} (hÏ‡ : Ï‡ â‰  1) :
    Differentiable â„‚ (LFunction Ï‡) :=
  (differentiableAt_LFunction _ Â· <| Or.inr hÏ‡)

/-- The L-function of an even Dirichlet character vanishes at strictly negative even integers. -/
@[simp]
lemma Even.LFunction_neg_two_mul_nat_add_one {Ï‡ : DirichletCharacter â„‚ N} (hÏ‡ : Even Ï‡) (n : â„•) :
    LFunction Ï‡ (-(2 * (n + 1))) = 0 :=
  ZMod.LFunction_neg_two_mul_nat_add_one hÏ‡.to_fun n

/-- The L-function of an even Dirichlet character vanishes at strictly negative even integers. -/
@[simp]
lemma Even.LFunction_neg_two_mul_nat {Ï‡ : DirichletCharacter â„‚ N} (hÏ‡ : Even Ï‡) (n : â„•) [NeZero n] :
    LFunction Ï‡ (-(2 * n)) = 0 := by
  obtain âŸ¨m, rflâŸ© := Nat.exists_eq_succ_of_ne_zero (NeZero.ne n)
  exact_mod_cast hÏ‡.LFunction_neg_two_mul_nat_add_one m

/-- The L-function of an odd Dirichlet character vanishes at negative odd integers. -/
@[simp] lemma Odd.LFunction_neg_two_mul_nat_sub_one
  {Ï‡ : DirichletCharacter â„‚ N} (hÏ‡ : Odd Ï‡) (n : â„•) :
    LFunction Ï‡ (-(2 * n) - 1) = 0 :=
  ZMod.LFunction_neg_two_mul_nat_sub_one hÏ‡.to_fun n

/-!
### Results on changing levels
-/

private lemma LFunction_changeLevel_aux {M N : â„•} [NeZero M] [NeZero N] (hMN : M âˆ£ N)
    (Ï‡ : DirichletCharacter â„‚ M) {s : â„‚} (hs : s â‰  1) :
    LFunction (changeLevel hMN Ï‡) s =
      LFunction Ï‡ s * âˆ p âˆˆ N.primeFactors, (1 - Ï‡ p * p ^ (-s)) := by
  have hpc : IsPreconnected ({1}á¶œ : Set â„‚) :=
    (isConnected_compl_singleton_of_one_lt_rank (rank_real_complex â–¸ Nat.one_lt_ofNat) _)
      |>.isPreconnected
  have hne : 2 âˆˆ ({1}á¶œ : Set â„‚) := by norm_num
  refine AnalyticOnNhd.eqOn_of_preconnected_of_eventuallyEq (ğ•œ := â„‚)
    (g := fun s â†¦ LFunction Ï‡ s * âˆ p âˆˆ N.primeFactors, (1 - Ï‡ p * p ^ (-s))) ?_ ?_ hpc hne ?_ hs
  Â· refine DifferentiableOn.analyticOnNhd (fun s hs â†¦ ?_) isOpen_compl_singleton
    exact (differentiableAt_LFunction _ _ (.inl hs)).differentiableWithinAt
  Â· refine DifferentiableOn.analyticOnNhd (fun s hs â†¦ ?_) isOpen_compl_singleton
    refine ((differentiableAt_LFunction _ _ (.inl hs)).mul ?_).differentiableWithinAt
    refine .finset_prod fun i h â†¦ ?_
    have : NeZero i := âŸ¨(Nat.pos_of_mem_primeFactors h).ne'âŸ©
    fun_prop
  Â· refine eventually_of_mem ?_  (fun t (ht : 1 < t.re) â†¦ ?_)
    Â· exact (continuous_re.isOpen_preimage _ isOpen_Ioi).mem_nhds (by norm_num : 1 < (2 : â„‚).re)
    Â· simpa [LFunction_eq_LSeries _ ht] using LSeries_changeLevel hMN Ï‡ ht

/-- If `Ï‡` is a Dirichlet character and its level `M` divides `N`, then we obtain the L function
of `Ï‡` considered as a Dirichlet character of level `N` from the L function of `Ï‡` by multiplying
with `âˆ p âˆˆ N.primeFactors, (1 - Ï‡ p * p ^ (-s))`.
(Note that `1 - Ï‡ p * p ^ (-s) = 1` when `p` divides `M`). -/
lemma LFunction_changeLevel {M N : â„•} [NeZero M] [NeZero N] (hMN : M âˆ£ N)
    (Ï‡ : DirichletCharacter â„‚ M) {s : â„‚} (h : Ï‡ â‰  1 âˆ¨ s â‰  1) :
    LFunction (changeLevel hMN Ï‡) s =
      LFunction Ï‡ s * âˆ p âˆˆ N.primeFactors, (1 - Ï‡ p * p ^ (-s)) := by
  rcases h with h | h
  Â· have hÏ‡ : changeLevel hMN Ï‡ â‰  1 := h âˆ˜ (changeLevel_eq_one_iff hMN).mp
    have h' : Continuous fun s â†¦ LFunction Ï‡ s * âˆ p âˆˆ N.primeFactors, (1 - Ï‡ p * â†‘p ^ (-s)) :=
      (differentiable_LFunction h).continuous.mul <| continuous_finset_prod _ fun p hp â†¦ by
        have : NeZero p := âŸ¨(Nat.prime_of_mem_primeFactors hp).ne_zeroâŸ©
        fun_prop
    exact congrFun ((differentiable_LFunction hÏ‡).continuous.ext_on
      (dense_compl_singleton 1) h' (fun _ h â†¦ LFunction_changeLevel_aux hMN Ï‡ h)) s
  Â· exact LFunction_changeLevel_aux hMN Ï‡ h

/-!
### The `L`-function of the trivial character mod `N`
-/

/-- The `L`-function of the trivial character mod `N`. -/
noncomputable abbrev LFunctionTrivChar (N : â„•) [NeZero N] :=
  (1 : DirichletCharacter â„‚ N).LFunction

/-- The L function of the trivial Dirichlet character mod `N` is obtained from the Riemann
zeta function by multiplying with `âˆ p âˆˆ N.primeFactors, (1 - (p : â„‚) ^ (-s))`. -/
lemma LFunctionTrivChar_eq_mul_riemannZeta {s : â„‚} (hs : s â‰  1) :
    LFunctionTrivChar N s = (âˆ p âˆˆ N.primeFactors, (1 - (p : â„‚) ^ (-s))) * riemannZeta s := by
  rw [â† LFunction_modOne_eq (Ï‡ := 1), LFunctionTrivChar, â† changeLevel_one N.one_dvd, mul_comm]
  convert LFunction_changeLevel N.one_dvd 1 (.inr hs) using 4 with p
  rw [MulChar.one_apply <| isUnit_of_subsingleton _, one_mul]

/-- The L function of the trivial Dirichlet character mod `N` has a simple pole with
residue `âˆ p âˆˆ N.primeFactors, (1 - pâ»Â¹)` at `s = 1`. -/
lemma LFunctionTrivChar_residue_one :
    Tendsto (fun s â†¦ (s - 1) * LFunctionTrivChar N s) (ğ“[â‰ ] 1)
      (ğ“ <| âˆ p âˆˆ N.primeFactors, (1 - (p : â„‚)â»Â¹)) := by
  have H : (fun s â†¦ (s - 1) * LFunctionTrivChar N s) =á¶ [ğ“[â‰ ] 1]
        fun s â†¦ (âˆ p âˆˆ N.primeFactors, (1 - (p : â„‚) ^ (-s))) * ((s - 1) * riemannZeta s) := by
    refine Set.EqOn.eventuallyEq_nhdsWithin fun s hs â†¦ ?_
    rw [mul_left_comm, LFunctionTrivChar_eq_mul_riemannZeta hs]
  rw [tendsto_congr' H]
  conv => enter [3, 1]; rw [â† mul_one <| Finset.prod ..]; enter [1, 2, p]; rw [â† cpow_neg_one]
  refine .mul (f := fun s â†¦ âˆ p âˆˆ N.primeFactors, _) ?_ riemannZeta_residue_one
  refine tendsto_nhdsWithin_of_tendsto_nhds <| Continuous.tendsto ?_ 1
  exact continuous_finset_prod _ fun p hp â†¦ by
    have : NeZero p := âŸ¨(Nat.prime_of_mem_primeFactors hp).ne_zeroâŸ©
    fun_prop

/-!
### Completed L-functions and the functional equation
-/

section gammaFactor

omit [NeZero N] -- not required for these declarations

open scoped Classical in
/-- The Archimedean Gamma factor: `Gammaâ„ s` if `Ï‡` is even, and `Gammaâ„ (s + 1)` otherwise. -/
noncomputable def gammaFactor (Ï‡ : DirichletCharacter â„‚ N) (s : â„‚) :=
  if Ï‡.Even then Gammaâ„ s else Gammaâ„ (s + 1)

lemma Even.gammaFactor_def {Ï‡ : DirichletCharacter â„‚ N} (hÏ‡ : Ï‡.Even) (s : â„‚) :
    gammaFactor Ï‡ s = Gammaâ„ s := by
  simp [gammaFactor, hÏ‡]

lemma Odd.gammaFactor_def {Ï‡ : DirichletCharacter â„‚ N} (hÏ‡ : Ï‡.Odd) (s : â„‚) :
    gammaFactor Ï‡ s = Gammaâ„ (s + 1) := by
  simp [gammaFactor, hÏ‡.not_even]

end gammaFactor

/--
The completed L-function of a Dirichlet character, almost everywhere equal to
`LFunction Ï‡ s * gammaFactor Ï‡ s`.
-/
@[pp_nodot] noncomputable def completedLFunction (Ï‡ : DirichletCharacter â„‚ N) (s : â„‚) : â„‚ :=
  ZMod.completedLFunction Ï‡ s

/--
The completed L-function of the (unique) Dirichlet character mod 1 is the completed Riemann zeta
function.
-/
lemma completedLFunction_modOne_eq {Ï‡ : DirichletCharacter â„‚ 1} :
    completedLFunction Ï‡ = completedRiemannZeta := by
  ext; rw [completedLFunction, ZMod.completedLFunction_modOne_eq, map_one, one_mul]

/--
The completed L-function of a Dirichlet character is differentiable, with the following
exceptions: at `s = 1` if `Ï‡` is the trivial character (to any modulus); and at `s = 0` if the
modulus is 1. This result is best possible.

Note both `Ï‡` and `s` are explicit arguments: we will always be able to infer one or other
of them from the hypotheses, but it's not clear which!
-/
lemma differentiableAt_completedLFunction (Ï‡ : DirichletCharacter â„‚ N) (s : â„‚)
    (hsâ‚€ : s â‰  0 âˆ¨ N â‰  1) (hsâ‚ : s â‰  1 âˆ¨ Ï‡ â‰  1) :
    DifferentiableAt â„‚ (completedLFunction Ï‡) s :=
  ZMod.differentiableAt_completedLFunction _ _ (by have := Ï‡.map_zero'; tauto)
    (by have := Ï‡.sum_eq_zero_of_ne_one; tauto)

/-- The completed L-function of a non-trivial Dirichlet character is differentiable everywhere. -/
lemma differentiable_completedLFunction {Ï‡ : DirichletCharacter â„‚ N} (hÏ‡ : Ï‡ â‰  1) :
    Differentiable â„‚ (completedLFunction Ï‡) := by
  refine fun s â†¦ differentiableAt_completedLFunction _ _ (Or.inr ?_) (Or.inr hÏ‡)
  exact hÏ‡ âˆ˜ level_one' _

/--
Relation between the completed L-function and the usual one. We state it this way around so
it holds at the poles of the gamma factor as well.
-/
lemma LFunction_eq_completed_div_gammaFactor (Ï‡ : DirichletCharacter â„‚ N) (s : â„‚)
    (h : s â‰  0 âˆ¨ N â‰  1) : LFunction Ï‡ s = completedLFunction Ï‡ s / gammaFactor Ï‡ s := by
  rcases Ï‡.even_or_odd with hÏ‡ | hÏ‡ <;>
  rw [hÏ‡.gammaFactor_def]
  Â· exact LFunction_eq_completed_div_gammaFactor_even hÏ‡.to_fun _ (h.imp_right Ï‡.map_zero')
  Â· apply LFunction_eq_completed_div_gammaFactor_odd hÏ‡.to_fun

open scoped Classical in
/--
Global root number of `Ï‡` (for `Ï‡` primitive; junk otherwise). Defined as
`gaussSum Ï‡ stdAddChar / I ^ a / N ^ (1 / 2)`, where `a = 0` if even, `a = 1` if odd. (The factor
`1 / I ^ a` is the Archimedean root number.) This is a complex number of absolute value 1.
-/
noncomputable def rootNumber (Ï‡ : DirichletCharacter â„‚ N) : â„‚ :=
  gaussSum Ï‡ stdAddChar / I ^ (if Ï‡.Even then 0 else 1) / N ^ (1 / 2 : â„‚)

/-- The root number of the unique Dirichlet character modulo 1 is 1. -/
lemma rootNumber_modOne (Ï‡ : DirichletCharacter â„‚ 1) : rootNumber Ï‡ = 1 := by
  simp [rootNumber, gaussSum, -univ_unique, â† singleton_eq_univ (1 : ZMod 1),
    (show stdAddChar (1 : ZMod 1) = 1 from AddChar.map_zero_eq_one _),
    (show Ï‡.Even from map_one _)]

namespace IsPrimitive

/-- **Functional equation** for primitive Dirichlet L-functions. -/
theorem completedLFunction_one_sub {Ï‡ : DirichletCharacter â„‚ N} (hÏ‡ : IsPrimitive Ï‡) (s : â„‚) :
    completedLFunction Ï‡ (1 - s) = N ^ (s - 1 / 2) * rootNumber Ï‡ * completedLFunction Ï‡â»Â¹ s := by
  classical
  -- First handle special case of Riemann zeta
  rcases eq_or_ne N 1 with rfl | hN
  Â· simp [completedLFunction_modOne_eq, completedRiemannZeta_one_sub, rootNumber_modOne]
  -- facts about `Ï‡` as function
  have h_sum : âˆ‘ j, Ï‡ j = 0 := by
    refine Ï‡.sum_eq_zero_of_ne_one (fun h â†¦ hN.symm ?_)
    rwa [IsPrimitive, h, conductor_one (NeZero.ne _)] at hÏ‡
  let Îµ := I ^ (if Ï‡.Even then 0 else 1)
  -- gather up powers of N
  rw [rootNumber, â† mul_comm_div, â† mul_comm_div, â† cpow_sub _ _ (NeZero.ne _), sub_sub, add_halves]
  calc completedLFunction Ï‡ (1 - s)
  _ = N ^ (s - 1) * Ï‡ (-1) /  Îµ * ZMod.completedLFunction (ğ“• Ï‡) s := by
    simp only [Îµ]
    split_ifs with h
    Â· rw [pow_zero, div_one, h, mul_one, completedLFunction,
        completedLFunction_one_sub_even h.to_fun _ (.inr h_sum) (.inr <| Ï‡.map_zero' hN)]
    Â· replace h : Ï‡.Odd := Ï‡.even_or_odd.resolve_left h
      rw [completedLFunction, completedLFunction_one_sub_odd h.to_fun,
        pow_one, h, div_I, mul_neg_one, â† neg_mul, neg_neg]
  _ = (_) * ZMod.completedLFunction (fun j â†¦ Ï‡â»Â¹ (-1) * gaussSum Ï‡ stdAddChar * Ï‡â»Â¹ j) s := by
    congr 2 with j
    rw [hÏ‡.fourierTransform_eq_inv_mul_gaussSum, â† neg_one_mul j, map_mul, mul_right_comm]
  _ = N ^ (s - 1) / Îµ * gaussSum Ï‡ stdAddChar * completedLFunction Ï‡â»Â¹ s * (Ï‡ (-1) * Ï‡â»Â¹ (-1)):= by
    rw [completedLFunction, completedLFunction_const_mul]
    ring
  _ = N ^ (s - 1) / Îµ * gaussSum Ï‡ stdAddChar * completedLFunction Ï‡â»Â¹ s := by
    rw [â† MulChar.mul_apply, mul_inv_cancel, MulChar.one_apply (isUnit_one.neg), mul_one]

end IsPrimitive

end DirichletCharacter

/-!
### The logarithmic derivative of the L-function of a Dirichlet character

We show that `s â†¦ -(L' Ï‡ s) / L Ï‡ s + 1 / (s - 1)` is continuous outside the zeros of `L Ï‡`
when `Ï‡` is a trivial Dirichlet character and that `-L' Ï‡ / L Ï‡` is continuous outside
the zeros of `L Ï‡` when `Ï‡` is nontrivial.
-/

namespace DirichletCharacter

open Complex

section trivial

variable (n : â„•) [NeZero n]

/-- The function obtained by "multiplying away" the pole of `L Ï‡` for a trivial Dirichlet
character `Ï‡`. Its (negative) logarithmic derivative is used to prove Dirichlet's Theorem
on primes in arithmetic progression. -/
noncomputable abbrev LFunctionTrivCharâ‚ : â„‚ â†’ â„‚ :=
  Function.update (fun s â†¦ (s - 1) * LFunctionTrivChar n s) 1
    (âˆ p âˆˆ n.primeFactors, (1 - (p : â„‚)â»Â¹))

lemma LFunctionTrivCharâ‚_apply_one_ne_zero : LFunctionTrivCharâ‚ n 1 â‰  0 := by
  simp only [Function.update_self]
  refine Finset.prod_ne_zero_iff.mpr fun p hp â†¦ ?_
  simpa [sub_ne_zero] using (Nat.prime_of_mem_primeFactors hp).ne_one

/-- `s â†¦ (s - 1) * L Ï‡ s` is an entire function when `Ï‡` is a trivial Dirichlet character. -/
lemma differentiable_LFunctionTrivCharâ‚ : Differentiable â„‚ (LFunctionTrivCharâ‚ n) := by
  rw [â† differentiableOn_univ,
    â† differentiableOn_compl_singleton_and_continuousAt_iff (c := 1) Filter.univ_mem]
  refine âŸ¨DifferentiableOn.congr (f := fun s â†¦ (s - 1) * LFunctionTrivChar n s)
    (fun _ hs â†¦ DifferentiableAt.differentiableWithinAt <| by fun_prop (disch := simp_all [hs]))
   fun _ hs â†¦ Function.update_of_ne (Set.mem_diff_singleton.mp hs).2 ..,
    continuousWithinAt_compl_self.mp ?_âŸ©
  simpa using LFunctionTrivChar_residue_one

lemma deriv_LFunctionTrivCharâ‚_apply_of_ne_one {s : â„‚} (hs : s â‰  1) :
    deriv (LFunctionTrivCharâ‚ n) s =
      (s - 1) * deriv (LFunctionTrivChar n) s + LFunctionTrivChar n s := by
  have H : deriv (LFunctionTrivCharâ‚ n) s =
      deriv (fun w â†¦ (w - 1) * LFunctionTrivChar n w) s := by
    refine eventuallyEq_iff_exists_mem.mpr ?_ |>.deriv_eq
    exact âŸ¨_, isOpen_ne.mem_nhds hs, fun _ hw â†¦ Function.update_of_ne (Set.mem_setOf.mp hw) ..âŸ©
  rw [H, deriv_mul (by fun_prop) (differentiableAt_LFunction _ s (.inl hs)), deriv_sub_const,
    deriv_id'', one_mul, add_comm]

/-- The negative logarithmtic derivative of `s â†¦ (s - 1) * L Ï‡ s` for a trivial
Dirichlet character `Ï‡` is continuous away from the zeros of `L Ï‡` (including at `s = 1`). -/
lemma continuousOn_neg_logDeriv_LFunctionTrivCharâ‚ :
    ContinuousOn (fun s â†¦ -deriv (LFunctionTrivCharâ‚ n) s / LFunctionTrivCharâ‚ n s)
      {s | s = 1 âˆ¨ LFunctionTrivChar n s â‰  0} := by
  simp_rw [neg_div]
  have h := differentiable_LFunctionTrivCharâ‚ n
  refine ((h.contDiff.continuous_deriv le_rfl).continuousOn.div
    h.continuous.continuousOn fun w hw â†¦ ?_).neg
  rcases eq_or_ne w 1 with rfl | hw'
  Â· exact LFunctionTrivCharâ‚_apply_one_ne_zero _
  Â· rw [LFunctionTrivCharâ‚, Function.update_of_ne hw', mul_ne_zero_iff]
    exact âŸ¨sub_ne_zero_of_ne hw', (Set.mem_setOf.mp hw).resolve_left hw'âŸ©

end trivial

section nontrivial

variable {n : â„•} [NeZero n] {Ï‡ : DirichletCharacter â„‚ n}

/-- The negative logarithmic derivative of the L-function of a nontrivial Dirichlet character
is continuous away from the zeros of the L-function. -/
lemma continuousOn_neg_logDeriv_LFunction_of_nontriv (hÏ‡ : Ï‡ â‰  1) :
    ContinuousOn (fun s â†¦ -deriv (LFunction Ï‡) s / LFunction Ï‡ s) {s | LFunction Ï‡ s â‰  0} := by
  have h := differentiable_LFunction hÏ‡
  simpa [neg_div] using ((h.contDiff.continuous_deriv le_rfl).continuousOn.div
    h.continuous.continuousOn fun _ hw â†¦ hw).neg

end nontrivial

end DirichletCharacter
