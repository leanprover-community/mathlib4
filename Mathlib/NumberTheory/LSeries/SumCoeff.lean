/-
Copyright (c) 2025 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.Analysis.Asymptotics.SpecificAsymptotics
import Mathlib.Analysis.InnerProductSpace.Calculus
import Mathlib.Analysis.SpecialFunctions.ImproperIntegrals
import Mathlib.NumberTheory.AbelSummation
import Mathlib.NumberTheory.LSeries.Basic

/-!

=======
# Partial sums of coefficients of L-series

We prove several results involving partial sums of coefficients (or norm of coefficients) of
L-series.

## Main results

* `LSeriesSummable_of_sum_norm_bigO`: for `f : â„• â†’ â„‚`, if the partial sums
  `âˆ‘ k âˆˆ Icc 1 n, â€–f kâ€–` are `O(n ^ r)` for some real `0 â‰¤ r`, then the L-series `LSeries f`
  converges at `s : â„‚` for all `s` such that `r < s.re`.

* `LSeries_eq_mul_integral` : for `f : â„• â†’ â„‚`, if the partial sums `âˆ‘ k âˆˆ Icc 1 n, f k` are
  `O(n ^ r)` for some real `0 â‰¤ r` and the L-series `LSeries f` converges at `s : â„‚` with
  `r < s.re`, then `LSeries f s = s * âˆ« t in Set.Ioi 1, (âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, f k) * t ^ (-(s + 1))`.

* `LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div` : assume that `f : â„• â†’ â„‚` satisfies that
  `(âˆ‘ k âˆˆ Icc 1 n, f k) / n` tends to some complex number `l` when `n â†’ âˆ` and that the L-series
  `LSeries f` converges for all `s : â„` such that `1 < s`. Then `(s - 1) * LSeries f s` tends
  to `l` when `s â†’ 1` with `1 < s`.

-/

open Finset Filter MeasureTheory Topology Complex Asymptotics

section summable

variable {f : â„• â†’ â„‚} {r : â„} {s : â„‚}

private theorem LSeriesSummable_of_sum_norm_bigO_aux (hf : f 0 = 0)
    (hO : (fun n â†¦ âˆ‘ k âˆˆ Icc 1 n, â€–f kâ€–) =O[atTop] fun n â†¦ (n : â„) ^ r)
    (hr : 0 â‰¤ r) (hs : r < s.re) :
    LSeriesSummable f s := by
  have hâ‚ : -s â‰  0 := neg_ne_zero.mpr <| ne_zero_of_re_pos (hr.trans_lt hs)
  have hâ‚‚ : (-s).re + r â‰¤ 0 := by
    rw [neg_re, neg_add_nonpos_iff]
    exact hs.le
  have hâ‚ƒ (t : â„) (ht : t âˆˆ Set.Ici 1) : DifferentiableAt â„ (fun x : â„ â†¦ â€–(x : â„‚) ^ (-s)â€–) t :=
    have ht' : t â‰  0 := (zero_lt_one.trans_le ht).ne'
    (differentiableAt_id.ofReal_cpow_const ht' hâ‚).norm â„ <|
      (cpow_ne_zero_iff_of_exponent_ne_zero hâ‚).mpr <| ofReal_ne_zero.mpr ht'
  have hâ‚„ : (deriv fun t : â„ â†¦ â€–(t : â„‚) ^ (-s)â€–) =á¶ [atTop] fun t â†¦ -s.re * t ^ (-(s.re +1)) := by
    filter_upwards [eventually_gt_atTop 0] with t ht
    rw [deriv_norm_ofReal_cpow _ ht, neg_re, neg_add']
  simp_rw [LSeriesSummable, funext (LSeries.term_defâ‚€ hf s), mul_comm (f _)]
  refine summable_mul_of_bigO_atTop' (f := fun t â†¦ (t : â„‚) ^ (-s))
    (g := fun t â†¦ t ^ (-(s.re + 1) + r)) _ hâ‚ƒ ?_ ?_ ?_ ?_
  Â· refine (integrableOn_Ici_iff_integrableOn_Ioi.mpr
      (integrableOn_Ioi_deriv_norm_ofReal_cpow zero_lt_one ?_)).locallyIntegrableOn
    exact neg_re _ â–¸ neg_nonpos.mpr  <| hr.trans hs.le
  Â· refine (IsBigO.mul_atTop_rpow_natCast_of_isBigO_rpow _ _ _ ?_ hO hâ‚‚).congr_right  (by simp)
    exact (norm_ofReal_cpow_eventually_eq_atTop _).isBigO.natCast_atTop
  Â· refine hâ‚„.isBigO.of_const_mul_right.mul_atTop_rpow_of_isBigO_rpow _ r _ ?_ le_rfl
    exact (hO.comp_tendsto tendsto_nat_floor_atTop).trans <|
      isEquivalent_nat_floor.isBigO.rpow hr (eventually_ge_atTop 0)
  Â· rwa [integrableAtFilter_rpow_atTop_iff, neg_add_lt_iff_lt_add, add_neg_cancel_right]

/-- If the partial sums `âˆ‘ k âˆˆ Icc 1 n, â€–f kâ€–` are `O(n ^ r)` for some real `0 â‰¤ r`, then the
L-series `LSeries f` converges at `s : â„‚` for all `s` such that `r < s.re`. -/
theorem LSeriesSummable_of_sum_norm_bigO
    (hO : (fun n â†¦ âˆ‘ k âˆˆ Icc 1 n, â€–f kâ€–) =O[atTop] fun n â†¦ (n : â„) ^ r)
    (hr : 0 â‰¤ r) (hs : r < s.re) :
    LSeriesSummable f s := by
  have hâ‚ : (fun n â†¦ if n = 0 then 0 else f n) =á¶ [atTop] f := by
    filter_upwards [eventually_ne_atTop 0] with n hn using by simp_rw [if_neg hn]
  refine (LSeriesSummable_of_sum_norm_bigO_aux (if_pos rfl) ?_ hr hs).congr' _ hâ‚
  refine hO.congr' (Eventually.of_forall fun _ â†¦ Finset.sum_congr rfl fun _ h â†¦ ?_) EventuallyEq.rfl
  rw [if_neg (zero_lt_one.trans_le (mem_Icc.mp h).1).ne']

/-- If `f` takes nonnegative real values and the partial sums `âˆ‘ k âˆˆ Icc 1 n, f k` are `O(n ^ r)`
for some real `0 â‰¤ r`, then the L-series `LSeries f` converges at `s : â„‚` for all `s`
such that `r < s.re`. -/
theorem LSeriesSummable_of_sum_norm_bigO_and_nonneg
    {f : â„• â†’ â„} (hO : (fun n â†¦ âˆ‘ k âˆˆ Icc 1 n, f k) =O[atTop] fun n â†¦ (n : â„) ^ r)
    (hf : âˆ€ n, 0 â‰¤ f n) (hr : 0 â‰¤ r) (hs : r < s.re) :
    LSeriesSummable (fun n â†¦ f n) s :=
  LSeriesSummable_of_sum_norm_bigO (by simpa [_root_.abs_of_nonneg (hf _)]) hr hs

end summable

section integralrepresentation

private theorem LSeries_eq_mul_integral_aux {f : â„• â†’ â„‚} (hf : f 0 = 0) {r : â„} (hr : 0 â‰¤ r) {s : â„‚}
    (hs : r < s.re) (hS : LSeriesSummable f s)
    (hO : (fun n â†¦ âˆ‘ k âˆˆ Icc 1 n, f k) =O[atTop] fun n â†¦ (n : â„) ^ r) :
    LSeries f s = s * âˆ« t in Set.Ioi (1 : â„), (âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, f k) * t ^ (- (s + 1)) := by
  have hâ‚ : (-s - 1).re + r < -1 := by
    rwa [sub_re, one_re, neg_re, neg_sub_left, neg_add_lt_iff_lt_add, add_neg_cancel_comm]
  have hâ‚‚ : s â‰  0 := ne_zero_of_re_pos (hr.trans_lt hs)
  have hâ‚ƒ (t : â„) (ht : t âˆˆ Set.Ici 1) : DifferentiableAt â„ (fun x : â„ â†¦ (x : â„‚) ^ (-s)) t :=
    differentiableAt_id.ofReal_cpow_const (zero_lt_one.trans_le ht).ne' (neg_ne_zero.mpr hâ‚‚)
  have hâ‚„ : âˆ€ n, âˆ‘ k âˆˆ Icc 0 n, f k = âˆ‘ k âˆˆ Icc 1 n, f k := fun n â†¦ by
    rw [â† Nat.Icc_insert_succ_left n.zero_le, sum_insert (by aesop), hf, zero_add, zero_add]
  simp_rw [â† hâ‚„] at hO
  rw [â† integral_mul_left]
  refine tendsto_nhds_unique ((tendsto_add_atTop_iff_nat 1).mpr hS.hasSum.tendsto_sum_nat) ?_
  simp_rw [Nat.range_succ_eq_Icc_zero, LSeries.term_defâ‚€ hf, mul_comm (f _)]
  convert tendsto_sum_mul_atTop_nhds_one_sub_integralâ‚€ (f := fun x â†¦ (x : â„‚) ^ (-s)) (l := 0)
    ?_ hf hâ‚ƒ ?_ ?_ ?_ (integrableAtFilter_rpow_atTop_iff.mpr hâ‚)
  Â· rw [zero_sub, â† integral_neg]
    refine setIntegral_congr_fun measurableSet_Ioi fun t ht â†¦ ?_
    rw [deriv_ofReal_cpow_const (zero_lt_one.trans ht).ne', hâ‚„]
    Â· ring_nf
    Â· exact neg_ne_zero.mpr <| ne_zero_of_re_pos (hr.trans_lt hs)
  Â· refine (integrableOn_Ici_iff_integrableOn_Ioi.mpr <|
      integrableOn_Ioi_deriv_ofReal_cpow zero_lt_one
        (by simpa using hr.trans_lt hs)).locallyIntegrableOn
  Â· have hlim : Tendsto (fun n : â„• â†¦ (n : â„) ^ (-(s.re - r))) atTop (ğ“ 0) :=
      (tendsto_rpow_neg_atTop (by rwa [sub_pos])).comp tendsto_natCast_atTop_atTop
    refine (IsBigO.mul_atTop_rpow_natCast_of_isBigO_rpow (-s.re) _ _ ?_ hO ?_).trans_tendsto hlim
    Â· exact isBigO_norm_left.mp <| (norm_ofReal_cpow_eventually_eq_atTop _).isBigO.natCast_atTop
    Â· linarith
  Â· refine .mul_atTop_rpow_of_isBigO_rpow (-(s + 1).re) r _ ?_ ?_ (by rw [â† neg_re, neg_add'])
    Â· simpa [- neg_add_rev, neg_add'] using isBigO_deriv_ofReal_cpow_const_atTop _
    Â· exact (hO.comp_tendsto tendsto_nat_floor_atTop).trans <|
        isEquivalent_nat_floor.isBigO.rpow hr (eventually_ge_atTop 0)

/-- If the partial sums `âˆ‘ k âˆˆ Icc 1 n, f k` are `O(n ^ r)` for some real `0 â‰¤ r` and the
L-series `LSeries f` converges at `s : â„‚` with `r < s.re`, then
`LSeries f s = s * âˆ« t in Set.Ioi 1, (âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, f k) * t ^ (-(s + 1))`. -/
theorem LSeries_eq_mul_integral (f : â„• â†’ â„‚) {r : â„} (hr : 0 â‰¤ r) {s : â„‚} (hs : r < s.re)
    (hS : LSeriesSummable f s)
    (hO : (fun n â†¦ âˆ‘ k âˆˆ Icc 1 n, f k) =O[atTop] fun n â†¦ (n : â„) ^ r) :
    LSeries f s = s * âˆ« t in Set.Ioi (1 : â„), (âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, f k) * t ^ (-(s + 1)) := by
  rw [â† LSeriesSummable_congr' s (f := fun n â†¦ if n = 0 then 0 else f n)
    (by filter_upwards [eventually_ne_atTop 0] with n h using if_neg h)] at hS
  have (n) : âˆ‘ k âˆˆ Icc 1 n, (if k = 0 then 0 else f k) = âˆ‘ k âˆˆ Icc 1 n, f k :=
    Finset.sum_congr rfl fun k hk â†¦ by rw [if_neg (zero_lt_one.trans_le (mem_Icc.mp hk).1).ne']
  rw [â† LSeries_congr _ (fun _ â†¦ if_neg _), LSeries_eq_mul_integral_aux (if_pos rfl) hr hs hS] <;>
  simp_all

/-- A version of `LSeries_eq_mul_integral` where we use the stronger condition that the partial sums
`âˆ‘ k âˆˆ Icc 1 n, â€–f kâ€–` are `O(n ^ r)` to deduce the integral representation. -/
theorem LSeries_eq_mul_integral' (f : â„• â†’ â„‚) {r : â„} (hr : 0 â‰¤ r) {s : â„‚} (hs : r < s.re)
    (hO : (fun n â†¦ âˆ‘ k âˆˆ Icc 1 n, â€–f kâ€–) =O[atTop] fun n â†¦ (n : â„) ^ r) :
    LSeries f s = s * âˆ« t in Set.Ioi (1 : â„), (âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, f k) * t ^ (-(s + 1)) :=
  LSeries_eq_mul_integral _ hr hs (LSeriesSummable_of_sum_norm_bigO hO hr hs) <|
    (isBigO_of_le _ fun _ â†¦ (norm_sum_le _ _).trans <| Real.le_norm_self _).trans hO

/-- If `f` takes nonnegative real values and the partial sums `âˆ‘ k âˆˆ Icc 1 n, f k` are `O(n ^ r)`
for some real `0 â‰¤ r`, then for `s : â„‚` with `r < s.re`, we have
`LSeries f s = s * âˆ« t in Set.Ioi 1, (âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, f k) * t ^ (-(s + 1))`. -/
theorem LSeries_eq_mul_integral_of_nonneg (f : â„• â†’ â„) {r : â„} (hr : 0 â‰¤ r) {s : â„‚} (hs : r < s.re)
    (hO : (fun n â†¦ âˆ‘ k âˆˆ Icc 1 n, f k) =O[atTop] fun n â†¦ (n : â„) ^ r) (hf : âˆ€ n, 0 â‰¤ f n) :
    LSeries (fun n â†¦ f n) s =
      s * âˆ« t in Set.Ioi (1 : â„), (âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, (f k : â„‚)) * t ^ (-(s + 1)) :=
  LSeries_eq_mul_integral' _ hr hs <| hO.congr_left fun _ â†¦ by simp [_root_.abs_of_nonneg (hf _)]

end integralrepresentation

noncomputable section residue

variable {f : â„• â†’ â„‚} {l : â„‚} (hlim : Tendsto (fun n : â„• â†¦ (âˆ‘ k âˆˆ Icc 1 n, f k) / n) atTop (ğ“ l))

section lemmas

include hlim in
private theorem lemmaâ‚ {s : â„} (hs : 1 < s) :
    IntegrableOn (fun t : â„ â†¦ (âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, f k) * (t : â„‚) ^ (-(s : â„‚) - 1)) (Set.Ici 1) := by
  have hâ‚ : LocallyIntegrableOn (fun t : â„ â†¦ (âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, f k) * (t : â„‚) ^ (-(s : â„‚) - 1))
        (Set.Ici 1) := by
    simp_rw [mul_comm]
    refine locallyIntegrableOn_mul_sum_Icc f zero_le_one ?_
    refine ContinuousOn.locallyIntegrableOn (fun t ht â†¦ ?_) measurableSet_Ici
    exact (continuousAt_ofReal_cpow_const _ _ <|
      Or.inr (zero_lt_one.trans_le ht).ne').continuousWithinAt
  have hâ‚‚ : (fun t : â„ â†¦ âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, f k) =O[atTop] fun t â†¦ t ^ (1 : â„) := by
    simp_rw [Real.rpow_one]
    refine IsBigO.trans_isEquivalent ?_ isEquivalent_nat_floor
    suffices Tendsto (fun n â†¦ (âˆ‘ k âˆˆ Icc 1 n, f k) / ((n : â„) ^ (1 : â„) : â„)) atTop (ğ“ l) by
      simpa using (isBigO_atTop_natCast_rpow_of_tendsto_div_rpow this).comp_tendsto
        tendsto_nat_floor_atTop
    simpa using hlim
  refine hâ‚.integrableOn_of_isBigO_atTop (g := fun t â†¦ t ^(-s)) ?_ ?_
  Â· refine IsBigO.mul_atTop_rpow_of_isBigO_rpow 1 (-s - 1) _ hâ‚‚ ?_ (by linarith)
    exact (norm_ofReal_cpow_eventually_eq_atTop _).isBigO.of_norm_left
  Â· rwa [integrableAtFilter_rpow_atTop_iff, neg_lt_neg_iff]

private theorem lemmaâ‚‚ {s T Îµ : â„} {S : â„ â†’ â„‚} (hs : 1 < s) (hT : 0 < T)
    (hSâ‚ : LocallyIntegrableOn (fun t â†¦ S t) (Set.Ici 1)) (hSâ‚‚ : âˆ€ t â‰¥ T, â€–S tâ€– â‰¤ Îµ * t) :
    IntegrableOn (fun t : â„ â†¦ â€–S tâ€– * (t ^ (-s - 1))) (Set.Ici 1) := by
  have h : LocallyIntegrableOn (fun t : â„ â†¦ â€–S tâ€– * (t ^ (-s - 1))) (Set.Ici 1) := by
    refine hSâ‚.norm.mul_continuousOn ?_ isLocallyClosed_Ici
    exact fun t ht â†¦ (Real.continuousAt_rpow_const _ _
      <| Or.inl (zero_lt_one.trans_le ht).ne').continuousWithinAt
  refine h.integrableOn_of_isBigO_atTop (g := fun t â†¦ t ^(-s)) (isBigO_iff.mpr âŸ¨Îµ, ?_âŸ©) ?_
  Â· filter_upwards [eventually_ge_atTop T] with t ht
    have ht' : 0 < t := hT.trans_le ht
    rw [norm_mul, norm_norm, show t ^ (-s) = t * t ^ (-s - 1) by
        rw [Real.rpow_sub ht', Real.rpow_one, mul_div_cancelâ‚€ _ ht'.ne'],
      norm_mul, Real.norm_of_nonneg ht'.le, â† mul_assoc]
    exact mul_le_mul_of_nonneg_right (hSâ‚‚ t ht) (norm_nonneg _)
  Â· exact integrableAtFilter_rpow_atTop_iff.mpr <| neg_lt_neg_iff.mpr hs

end lemmas

section proof

include hlim in
private theorem LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div_auxâ‚ {Îµ : â„} (hÎµ : Îµ > 0) :
    âˆ€á¶  t : â„ in atTop, â€–(âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, f k) - l * tâ€– < Îµ * t := by
  have h_lim' : Tendsto (fun t : â„ â†¦ (âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, f k : â„‚) / t) atTop (ğ“ l) := by
    refine (mul_one l â–¸ ofReal_one â–¸ ((hlim.comp tendsto_nat_floor_atTop).mul <|
      tendsto_ofReal_iff.mpr <| tendsto_nat_floor_div_atTop)).congr' ?_
    filter_upwards [eventually_ge_atTop 1] with t ht
    rw [Function.comp_def, ofReal_div, ofReal_natCast, div_mul_div_cancelâ‚€ (by
      rwa [Nat.cast_ne_zero, ne_eq, Nat.floor_eq_zero, not_lt])]
  rw [Metric.tendsto_nhds] at h_lim'
  filter_upwards [eventually_gt_atTop 0, h_lim' Îµ hÎµ] with t htâ‚ htâ‚‚
  rwa [dist_eq_norm, div_sub' _ _ _ (ne_zero_of_re_pos htâ‚), norm_div, norm_real,
    Real.norm_of_nonneg htâ‚.le, mul_comm, div_lt_iffâ‚€ htâ‚] at htâ‚‚

private theorem LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div_auxâ‚‚ {s T Îµ : â„} {S : â„ â†’ â„‚}
    (hS : LocallyIntegrableOn (fun t â†¦ S t - l * t) (Set.Ici 1)) (hÎµ : 0 < Îµ)
    (hs : 1 < s) (hTâ‚ : 1 â‰¤ T) (hT : âˆ€ t â‰¥ T, â€–S t - l * tâ€– â‰¤ Îµ * t) :
    (s - 1) * âˆ« (t : â„) in Set.Ioi T, â€–S t - l * tâ€– * t ^ (-s - 1) â‰¤ Îµ := by
  have hTâ‚€ : 0 < T := zero_lt_one.trans_le hTâ‚
  have h {t : â„} (ht : 0 < t) : t ^ (-s) = t * t ^ (-s - 1) := by
    rw [Real.rpow_sub ht, Real.rpow_one, mul_div_cancelâ‚€ _ ht.ne']
  calc
    _ â‰¤ (s - 1) * âˆ« (t : â„) in Set.Ioi T, Îµ * t ^ (-s) := ?_
    _ â‰¤ Îµ * ((s - 1) * âˆ« (t : â„) in Set.Ioi 1, t ^ (-s)) := ?_
    _ = Îµ := ?_
  Â· refine mul_le_mul_of_nonneg_left (setIntegral_mono_on ?_ ?_ measurableSet_Ioi fun t ht â†¦ ?_) ?_
    Â· exact (lemmaâ‚‚ hs hTâ‚€ hS hT).mono_set <| Set.Ioi_subset_Ici_iff.mpr hTâ‚
    Â· exact (integrableOn_Ioi_rpow_of_lt (neg_lt_neg_iff.mpr hs) hTâ‚€).const_mul  _
    Â· have ht' : 0 < t := hTâ‚€.trans ht
      rw [h ht', â† mul_assoc]
      exact mul_le_mul_of_nonneg_right (hT t ht.le) (Real.rpow_nonneg ht'.le _)
    Â· exact (sub_pos_of_lt hs).le
  Â· rw [integral_mul_left, â† mul_assoc, â† mul_assoc, mul_comm Îµ]
    refine mul_le_mul_of_nonneg_left (setIntegral_mono_set ?_ ?_
      (Set.Ioi_subset_Ioi hTâ‚).eventuallyLE) (mul_nonneg (sub_pos_of_lt hs).le hÎµ.le)
    Â· exact integrableOn_Ioi_rpow_of_lt (neg_lt_neg_iff.mpr hs) zero_lt_one
    Â· exact (ae_restrict_iff' measurableSet_Ioi).mpr <| univ_mem' fun t ht â†¦
        Real.rpow_nonneg (zero_le_one.trans ht.le) _
  Â· rw [integral_Ioi_rpow_of_lt (by rwa [neg_lt_neg_iff]) zero_lt_one, Real.one_rpow, neg_div,
      â† one_div_neg_eq_neg_one_div, neg_add', neg_neg, mul_one_div,
      div_self (sub_ne_zero.mpr hs.ne'), mul_one]

variable (hfS : âˆ€ s : â„, 1 < s â†’ LSeriesSummable f s)

include hlim hfS in
private theorem LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div_auxâ‚ƒ {Îµ : â„} (hÎµ : Îµ > 0) :
    âˆƒ C â‰¥ 0, âˆ€ s : â„, 1 < s â†’ â€–(s - 1) * LSeries f s - l * sâ€– â‰¤ (s - 1) * s * C + s * Îµ := by
  obtain âŸ¨T, hTâ‚, hTâŸ© := (eventually_forall_ge_atTop.mpr
    (LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div_auxâ‚
      hlim hÎµ)).frequently.forall_exists_of_atTop 1
  let S : â„ â†’ â„‚ := fun t â†¦ âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, f k
  let C := âˆ« t in Set.Ioc 1 T, â€–S t - l * tâ€– * t ^ (-1 - 1 : â„)
  have hC : 0 â‰¤ C := by
    refine setIntegral_nonneg_ae measurableSet_Ioc (univ_mem' fun t ht â†¦ ?_)
    exact mul_nonneg (norm_nonneg _) <| Real.rpow_nonneg (zero_le_one.trans ht.1.le) _
  refine âŸ¨C, hC, fun s hs â†¦ ?_âŸ©
  have hs' : 0 â‰¤ (s - 1) * s := mul_nonneg (sub_nonneg.mpr hs.le) (zero_le_one.trans hs.le)
  have hâ‚€ : LocallyIntegrableOn (fun t â†¦ S t - l * t) (Set.Ici 1) := by
    refine .sub ?_ <| ContinuousOn.locallyIntegrableOn (by fun_prop) measurableSet_Ici
    convert locallyIntegrableOn_mul_sum_Icc f zero_le_one (locallyIntegrableOn_const 1)
    rw [one_mul]
  have hâ‚ : IntegrableOn (fun t â†¦ â€–S t - l * tâ€– * t ^ (-s - 1)) (Set.Ici 1) :=
    lemmaâ‚‚ hs (zero_lt_one.trans_le hTâ‚) hâ‚€ fun t ht â†¦ (hT t ht).le
  have hâ‚‚ : IntegrableOn (fun t : â„ â†¦ â€–S t - l * tâ€– * (t ^ ((-1 : â„) - 1))) (Set.Ioc 1 T) := by
    refine ((hâ‚€.norm.mul_continuousOn ?_ isLocallyClosed_Ici).integrableOn_compact_subset
      Set.Icc_subset_Ici_self isCompact_Icc).mono_set Set.Ioc_subset_Icc_self
    exact fun t ht â†¦ (Real.continuousAt_rpow_const _ _
      <| Or.inl (zero_lt_one.trans_le ht).ne').continuousWithinAt
  have hâ‚ƒ : (s - 1) * âˆ« (t : â„) in Set.Ioi 1, (t : â„‚) ^ (-s : â„‚) = 1 := by
    rw [integral_Ioi_cpow_of_lt (by rwa [neg_re, neg_lt_neg_iff]) zero_lt_one, ofReal_one,
      one_cpow, show -(s : â„‚) + 1 = -(s - 1) by ring, neg_div_neg_eq, mul_div_cancelâ‚€]
    exact (sub_ne_zero.trans ofReal_ne_one).mpr hs.ne'
  let Cs := âˆ« t in Set.Ioc 1 T, â€–S t - l * tâ€– * t ^ (-s - 1)
  have hâ‚„ : Cs â‰¤ C := by
    refine setIntegral_mono_on ?_ hâ‚‚ measurableSet_Ioc fun t ht â†¦ ?_
    Â· exact hâ‚.mono_set <| Set.Ioc_subset_Ioi_self.trans Set.Ioi_subset_Ici_self
    Â· gcongr
      exact ht.1.le
  calc
    _ = â€–((s - 1) * s * âˆ« t in Set.Ioi 1, S t * â†‘t ^ (-(s : â„‚) - 1)) -
          l * s * ((s - 1) * âˆ« (t : â„) in Set.Ioi 1, â†‘t ^ (-(s : â„‚)))â€– := ?_
    _ = â€–(s - 1) * s *
          âˆ« (t : â„) in Set.Ioi 1, (S t * (t : â„‚) ^ (-s - 1 : â„‚) - l * t ^ (-s : â„‚))â€– := ?_
    _ = â€–(s - 1) * s * âˆ« (t : â„) in Set.Ioi 1, (S t - l * t) * (t : â„‚) ^ (-s - 1 : â„‚)â€– := ?_
    _ â‰¤ (s - 1) * s * âˆ« (t : â„) in Set.Ioi 1, â€–(S t - l * â†‘t) * â†‘t ^ (-s - 1 : â„‚)â€– := ?_
    _ = (s - 1) * s * âˆ« (t : â„) in Set.Ioi 1, â€–S t - l * tâ€– * t ^ (-s - 1) := ?_
    _ = (s - 1) * s * (Cs + âˆ« (t : â„) in Set.Ioi T, â€–S t - l * tâ€– * t ^ (-s - 1)) := ?_
    _ â‰¤ (s - 1) * s * C +
          s * ((s - 1) * âˆ« (t : â„) in Set.Ioi T, â€–S t - l * tâ€– * t ^ (-s - 1)) := ?_
    _ â‰¤ (s - 1) * s * C + s * Îµ := ?_
  Â· rw [hâ‚ƒ, mul_one, mul_comm l, LSeries_eq_mul_integral _ zero_le_one (by rwa [ofReal_re])
      (hfS _ hs), neg_add', mul_assoc]
    exact isBigO_atTop_natCast_rpow_of_tendsto_div_rpow (a := l) (by simpa using hlim)
  Â· rw [integral_sub, integral_mul_left]
    Â· congr; ring
    Â· exact (lemmaâ‚ hlim hs).mono_set Set.Ioi_subset_Ici_self
    Â· exact (integrableOn_Ioi_cpow_of_lt
        (by rwa [neg_re, ofReal_re, neg_lt_neg_iff]) zero_lt_one).const_mul  _
  Â· congr 2
    refine setIntegral_congr_fun measurableSet_Ioi fun t ht â†¦ ?_
    replace ht : (t : â„‚) â‰  0 := ne_zero_of_one_lt_re ht
    rw [sub_mul, cpow_sub _ _ ht, cpow_one, mul_assoc, mul_div_cancelâ‚€ _ ht]
  Â· rw [norm_mul, show ((s : â„‚) - 1) * s = ((s - 1) * s : â„) by simp, norm_real,
      Real.norm_of_nonneg hs']
    exact mul_le_mul_of_nonneg_left (norm_integral_le_integral_norm _) hs'
  Â· congr 1
    refine setIntegral_congr_fun measurableSet_Ioi fun t ht â†¦ ?_
    replace ht : 0 â‰¤ t := zero_le_one.trans ht.le
    rw [norm_mul, show (-(s : â„‚) - 1) = (-s - 1 : â„) by simp, â† ofReal_cpow ht, norm_real,
      Real.norm_of_nonneg (Real.rpow_nonneg ht _)]
  Â· rw [â† Set.Ioc_union_Ioi_eq_Ioi hTâ‚, setIntegral_union (Set.Ioc_disjoint_Ioi le_rfl)
      measurableSet_Ioi]
    Â· exact hâ‚.mono_set <| Set.Ioc_subset_Ioi_self.trans Set.Ioi_subset_Ici_self
    Â· exact hâ‚.mono_set <| Set.Ioi_subset_Ici_self.trans <| Set.Ici_subset_Ici.mpr hTâ‚
  Â· rw [mul_add, â† mul_assoc, mul_comm s]
    exact add_le_add_right (mul_le_mul_of_nonneg_left hâ‚„ hs') _
  Â· gcongr
    exact LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div_auxâ‚‚ hâ‚€ hÎµ hs hTâ‚
      fun t ht â†¦ (hT t ht.le).le

include hlim hfS in
theorem LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div :
    Tendsto (fun s : â„ â†¦ (s - 1) * LSeries f s) (ğ“[>] 1) (ğ“ l) := by
  refine Metric.tendsto_nhdsWithin_nhds.mpr fun Îµ hÎµ â†¦ ?_
  have hÎµ6 : 0 < Îµ / 6 := by positivity
  have hÎµ3 : 0 < Îµ / 3 := by positivity
  obtain âŸ¨C, hCâ‚, hCâ‚‚âŸ© := LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div_auxâ‚ƒ hlim hfS hÎµ6
  have lim1 : Tendsto (fun s â†¦ (s - 1) * s * C) (ğ“[>] 1) (ğ“ 0) := by
    have : ContinuousAt (fun s â†¦ (s - 1) * s * C) 1 := by fun_prop
    convert tendsto_nhdsWithin_of_tendsto_nhds this.tendsto
    rw [sub_self, zero_mul, zero_mul]
  have lim2 : Tendsto (fun s : â„ â†¦ s * l) (ğ“[>] 1) (ğ“ l) := by
    have : ContinuousAt (fun s : â„ â†¦ s * l) 1 := by fun_prop
    convert tendsto_nhdsWithin_of_tendsto_nhds this.tendsto
    rw [Complex.ofReal_one, one_mul]
  rw [Metric.tendsto_nhdsWithin_nhds] at lim1 lim2
  obtain âŸ¨Î´â‚, _, hÎ´â‚âŸ© := lim1 _ hÎµ3
  obtain âŸ¨Î´â‚‚, _, hÎ´â‚‚âŸ© := lim2 _ hÎµ3
  refine âŸ¨min 1 (min Î´â‚ Î´â‚‚), by positivity, fun s hsâ‚ hsâ‚‚ â†¦ ?_âŸ©
  specialize hCâ‚‚ s hsâ‚
  specialize hÎ´â‚ hsâ‚ <| hsâ‚‚.trans_le <| (min_le_right _ _).trans (min_le_left _ _)
  specialize hÎ´â‚‚ hsâ‚ <| hsâ‚‚.trans_le <| (min_le_right _ _).trans (min_le_right _ _)
  rw [dist_eq_norm] at hÎ´â‚ hÎ´â‚‚ hsâ‚‚ âŠ¢
  rw [sub_zero, Real.norm_of_nonneg (mul_nonneg
    (mul_nonneg (sub_nonneg.mpr hsâ‚.le) (zero_lt_one.trans hsâ‚).le) hCâ‚)] at hÎ´â‚
  have sle2 : s < 2 := by
    have := (abs_lt.mp <| Real.norm_eq_abs _ â–¸ (hsâ‚‚.trans_le (min_le_left _ _))).2
    rwa [sub_lt_iff_lt_add', one_add_one_eq_two] at this
  calc
    _ â‰¤ â€–(s - 1) * LSeries f s - l * sâ€– + â€–l * s - lâ€– := norm_sub_le_norm_sub_add_norm_sub _ _ _
    _ < (s - 1) * s * C + s * (Îµ / 6) + (Îµ / 3) := add_lt_add_of_le_of_lt hCâ‚‚ (by rwa [mul_comm])
    _ â‰¤ (Îµ / 3) + s * (Îµ / 6) + (Îµ / 3) := by gcongr
    _ < (Îµ / 3) + (Îµ / 3) + (Îµ / 3) := ?_
    _ = Îµ := add_thirds Îµ
  refine add_lt_add_right (add_lt_add_left ?_ (Îµ / 3)) (Îµ / 3)
  exact lt_of_lt_of_eq ((mul_lt_mul_right hÎµ6).mpr sle2) (by ring)

theorem LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div_and_nonneg (f : â„• â†’ â„) {l : â„}
    (hf : Tendsto (fun n â†¦ (âˆ‘ k âˆˆ Icc 1 n, f k) / (n : â„)) atTop (ğ“ l))
    (hf' : âˆ€ n, 0 â‰¤ f n) :
    Tendsto (fun s : â„ â†¦ (s - 1) * LSeries (fun n â†¦ f n) s) (ğ“[>] 1) (ğ“ l) := by
  refine  LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div (f := fun n â†¦ f n)
    (hf.ofReal.congr fun _ â†¦ ?_) fun s hs â†¦ ?_
  Â· simp_rw [ofReal_div, ofReal_sum, ofReal_natCast]
  Â· refine LSeriesSummable_of_sum_norm_bigO_and_nonneg ?_ hf' zero_le_one (by rwa [ofReal_re])
    exact isBigO_atTop_natCast_rpow_of_tendsto_div_rpow (a := l) (by simpa using hf)

end proof

end residue
