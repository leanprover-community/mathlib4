/-
Copyright (c) 2025 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.Analysis.Asymptotics.SpecificAsymptotics
import Mathlib.Analysis.InnerProductSpace.Calculus
import Mathlib.Analysis.SpecialFunctions.ImproperIntegrals
import Mathlib.NumberTheory.AbelSummation
import Mathlib.NumberTheory.LSeries.Basic

/-!
# Partial sums of coefficients of L-series

We prove several results involving partial sums of coefficients (or norm of coefficients) of
L-series.

## Main results

* `LSeriesSummable_of_sum_norm_bigO`: for `f : ‚Ñï ‚Üí ‚ÑÇ`, if the partial sums
  `‚àë k ‚àà Icc 1 n, ‚Äñf k‚Äñ` are `O(n ^ r)` for some real `0 ‚â§ r`, then the L-series `LSeries f`
  converges at `s : ‚ÑÇ` for all `s` such that `r < s.re`.

* `LSeries_eq_mul_integral` : for `f : ‚Ñï ‚Üí ‚ÑÇ`, if the partial sums `‚àë k ‚àà Icc 1 n, f k` are
  `O(n ^ r)` for some real `0 ‚â§ r` and the L-series `LSeries f` converges at `s : ‚ÑÇ` with
  `r < s.re`, then `LSeries f s = s * ‚à´ t in Set.Ioi 1, (‚àë k ‚àà Icc 1 ‚åät‚åã‚Çä, f k) * t ^ (-(s + 1))`.

* `LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div` : assume that `f : ‚Ñï ‚Üí ‚ÑÇ` satisfies that
  `(‚àë k ‚àà Icc 1 n, f k) / n` tends to some complex number `l` when `n ‚Üí ‚àû` and that the L-series
  `LSeries f` converges for all `s : ‚Ñù` such that `1 < s`. Then `(s - 1) * LSeries f s` tends
  to `l` when `s ‚Üí 1` with `1 < s`.

-/

open Finset Filter MeasureTheory Topology Complex Asymptotics

section summable

variable {f : ‚Ñï ‚Üí ‚ÑÇ} {r : ‚Ñù} {s : ‚ÑÇ}

private theorem LSeriesSummable_of_sum_norm_bigO_aux (hf : f 0 = 0)
    (hO : (fun n ‚Ü¶ ‚àë k ‚àà Icc 1 n, ‚Äñf k‚Äñ) =O[atTop] fun n ‚Ü¶ (n : ‚Ñù) ^ r)
    (hr : 0 ‚â§ r) (hs : r < s.re) :
    LSeriesSummable f s := by
  have h‚ÇÅ : -s ‚â† 0 := neg_ne_zero.mpr <| ne_zero_of_re_pos (hr.trans_lt hs)
  have h‚ÇÇ : (-s).re + r ‚â§ 0 := by
    rw [neg_re, neg_add_nonpos_iff]
    exact hs.le
  have h‚ÇÉ (t : ‚Ñù) (ht : t ‚àà Set.Ici 1) : DifferentiableAt ‚Ñù (fun x : ‚Ñù ‚Ü¶ ‚Äñ(x : ‚ÑÇ) ^ (-s)‚Äñ) t :=
    have ht' : t ‚â† 0 := (zero_lt_one.trans_le ht).ne'
    (differentiableAt_id.ofReal_cpow_const ht' h‚ÇÅ).norm ‚Ñù <|
      (cpow_ne_zero_iff_of_exponent_ne_zero h‚ÇÅ).mpr <| ofReal_ne_zero.mpr ht'
  have h‚ÇÑ : (deriv fun t : ‚Ñù ‚Ü¶ ‚Äñ(t : ‚ÑÇ) ^ (-s)‚Äñ) =·∂†[atTop] fun t ‚Ü¶ -s.re * t ^ (-(s.re +1)) := by
    filter_upwards [eventually_gt_atTop 0] with t ht
    rw [deriv_norm_ofReal_cpow _ ht, neg_re, neg_add']
  simp_rw [LSeriesSummable, funext (LSeries.term_def‚ÇÄ hf s), mul_comm (f _)]
  refine summable_mul_of_bigO_atTop' (f := fun t ‚Ü¶ (t : ‚ÑÇ) ^ (-s))
    (g := fun t ‚Ü¶ t ^ (-(s.re + 1) + r)) _ h‚ÇÉ ?_ ?_ ?_ ?_
  ¬∑ refine (integrableOn_Ici_iff_integrableOn_Ioi.mpr
      (integrableOn_Ioi_deriv_norm_ofReal_cpow zero_lt_one ?_)).locallyIntegrableOn
    exact neg_re _ ‚ñ∏ neg_nonpos.mpr  <| hr.trans hs.le
  ¬∑ refine (IsBigO.mul_atTop_rpow_natCast_of_isBigO_rpow _ _ _ ?_ hO h‚ÇÇ).congr_right  (by simp)
    exact (norm_ofReal_cpow_eventually_eq_atTop _).isBigO.natCast_atTop
  ¬∑ refine h‚ÇÑ.isBigO.of_const_mul_right.mul_atTop_rpow_of_isBigO_rpow _ r _ ?_ le_rfl
    exact (hO.comp_tendsto tendsto_nat_floor_atTop).trans <|
      isEquivalent_nat_floor.isBigO.rpow hr (eventually_ge_atTop 0)
  ¬∑ rwa [integrableAtFilter_rpow_atTop_iff, neg_add_lt_iff_lt_add, add_neg_cancel_right]

/-- If the partial sums `‚àë k ‚àà Icc 1 n, ‚Äñf k‚Äñ` are `O(n ^ r)` for some real `0 ‚â§ r`, then the
L-series `LSeries f` converges at `s : ‚ÑÇ` for all `s` such that `r < s.re`. -/
theorem LSeriesSummable_of_sum_norm_bigO
    (hO : (fun n ‚Ü¶ ‚àë k ‚àà Icc 1 n, ‚Äñf k‚Äñ) =O[atTop] fun n ‚Ü¶ (n : ‚Ñù) ^ r)
    (hr : 0 ‚â§ r) (hs : r < s.re) :
    LSeriesSummable f s := by
  have h‚ÇÅ : (fun n ‚Ü¶ if n = 0 then 0 else f n) =·∂†[atTop] f := by
    filter_upwards [eventually_ne_atTop 0] with n hn using by simp_rw [if_neg hn]
  refine (LSeriesSummable_of_sum_norm_bigO_aux (if_pos rfl) ?_ hr hs).congr' _ h‚ÇÅ
  refine hO.congr' (Eventually.of_forall fun _ ‚Ü¶ Finset.sum_congr rfl fun _ h ‚Ü¶ ?_) EventuallyEq.rfl
  rw [if_neg (zero_lt_one.trans_le (mem_Icc.mp h).1).ne']

/-- If `f` takes nonnegative real values and the partial sums `‚àë k ‚àà Icc 1 n, f k` are `O(n ^ r)`
for some real `0 ‚â§ r`, then the L-series `LSeries f` converges at `s : ‚ÑÇ` for all `s`
such that `r < s.re`. -/
theorem LSeriesSummable_of_sum_norm_bigO_and_nonneg
    {f : ‚Ñï ‚Üí ‚Ñù} (hO : (fun n ‚Ü¶ ‚àë k ‚àà Icc 1 n, f k) =O[atTop] fun n ‚Ü¶ (n : ‚Ñù) ^ r)
    (hf : ‚àÄ n, 0 ‚â§ f n) (hr : 0 ‚â§ r) (hs : r < s.re) :
    LSeriesSummable (fun n ‚Ü¶ f n) s :=
  LSeriesSummable_of_sum_norm_bigO (by simpa [abs_of_nonneg (hf _)]) hr hs

end summable

section integralrepresentation

private theorem LSeries_eq_mul_integral_aux {f : ‚Ñï ‚Üí ‚ÑÇ} (hf : f 0 = 0) {r : ‚Ñù} (hr : 0 ‚â§ r) {s : ‚ÑÇ}
    (hs : r < s.re) (hS : LSeriesSummable f s)
    (hO : (fun n ‚Ü¶ ‚àë k ‚àà Icc 1 n, f k) =O[atTop] fun n ‚Ü¶ (n : ‚Ñù) ^ r) :
    LSeries f s = s * ‚à´ t in Set.Ioi (1 : ‚Ñù), (‚àë k ‚àà Icc 1 ‚åät‚åã‚Çä, f k) * t ^ (- (s + 1)) := by
  have h‚ÇÅ : (-s - 1).re + r < -1 := by
    rwa [sub_re, one_re, neg_re, neg_sub_left, neg_add_lt_iff_lt_add, add_neg_cancel_comm]
  have h‚ÇÇ : s ‚â† 0 := ne_zero_of_re_pos (hr.trans_lt hs)
  have h‚ÇÉ (t : ‚Ñù) (ht : t ‚àà Set.Ici 1) : DifferentiableAt ‚Ñù (fun x : ‚Ñù ‚Ü¶ (x : ‚ÑÇ) ^ (-s)) t :=
    differentiableAt_id.ofReal_cpow_const (zero_lt_one.trans_le ht).ne' (neg_ne_zero.mpr h‚ÇÇ)
  have h‚ÇÑ : ‚àÄ n, ‚àë k ‚àà Icc 0 n, f k = ‚àë k ‚àà Icc 1 n, f k := fun n ‚Ü¶ by
    rw [‚Üê insert_Icc_add_one_left_eq_Icc n.zero_le, sum_insert (by aesop), hf, zero_add, zero_add]
  simp_rw [‚Üê h‚ÇÑ] at hO
  rw [‚Üê integral_const_mul]
  refine tendsto_nhds_unique ((tendsto_add_atTop_iff_nat 1).mpr hS.hasSum.tendsto_sum_nat) ?_
  simp_rw [Nat.range_succ_eq_Icc_zero, LSeries.term_def‚ÇÄ hf, mul_comm (f _)]
  convert tendsto_sum_mul_atTop_nhds_one_sub_integral‚ÇÄ (f := fun x ‚Ü¶ (x : ‚ÑÇ) ^ (-s)) (l := 0)
    ?_ hf h‚ÇÉ ?_ ?_ ?_ (integrableAtFilter_rpow_atTop_iff.mpr h‚ÇÅ)
  ¬∑ rw [zero_sub, ‚Üê integral_neg]
    refine setIntegral_congr_fun measurableSet_Ioi fun t ht ‚Ü¶ ?_
    rw [deriv_ofReal_cpow_const (zero_lt_one.trans ht).ne', h‚ÇÑ]
    ¬∑ ring_nf
    ¬∑ exact neg_ne_zero.mpr <| ne_zero_of_re_pos (hr.trans_lt hs)
  ¬∑ refine (integrableOn_Ici_iff_integrableOn_Ioi.mpr <|
      integrableOn_Ioi_deriv_ofReal_cpow zero_lt_one
        (by simpa using hr.trans_lt hs)).locallyIntegrableOn
  ¬∑ have hlim : Tendsto (fun n : ‚Ñï ‚Ü¶ (n : ‚Ñù) ^ (-(s.re - r))) atTop (ùìù 0) :=
      (tendsto_rpow_neg_atTop (by rwa [sub_pos])).comp tendsto_natCast_atTop_atTop
    refine (IsBigO.mul_atTop_rpow_natCast_of_isBigO_rpow (-s.re) _ _ ?_ hO ?_).trans_tendsto hlim
    ¬∑ exact isBigO_norm_left.mp <| (norm_ofReal_cpow_eventually_eq_atTop _).isBigO.natCast_atTop
    ¬∑ linarith
  ¬∑ refine .mul_atTop_rpow_of_isBigO_rpow (-(s + 1).re) r _ ?_ ?_ (by rw [‚Üê neg_re, neg_add'])
    ¬∑ simpa [- neg_add_rev, neg_add'] using isBigO_deriv_ofReal_cpow_const_atTop _
    ¬∑ exact (hO.comp_tendsto tendsto_nat_floor_atTop).trans <|
        isEquivalent_nat_floor.isBigO.rpow hr (eventually_ge_atTop 0)

/-- If the partial sums `‚àë k ‚àà Icc 1 n, f k` are `O(n ^ r)` for some real `0 ‚â§ r` and the
L-series `LSeries f` converges at `s : ‚ÑÇ` with `r < s.re`, then
`LSeries f s = s * ‚à´ t in Set.Ioi 1, (‚àë k ‚àà Icc 1 ‚åät‚åã‚Çä, f k) * t ^ (-(s + 1))`. -/
theorem LSeries_eq_mul_integral (f : ‚Ñï ‚Üí ‚ÑÇ) {r : ‚Ñù} (hr : 0 ‚â§ r) {s : ‚ÑÇ} (hs : r < s.re)
    (hS : LSeriesSummable f s)
    (hO : (fun n ‚Ü¶ ‚àë k ‚àà Icc 1 n, f k) =O[atTop] fun n ‚Ü¶ (n : ‚Ñù) ^ r) :
    LSeries f s = s * ‚à´ t in Set.Ioi (1 : ‚Ñù), (‚àë k ‚àà Icc 1 ‚åät‚åã‚Çä, f k) * t ^ (-(s + 1)) := by
  rw [‚Üê LSeriesSummable_congr' s (f := fun n ‚Ü¶ if n = 0 then 0 else f n)
    (by filter_upwards [eventually_ne_atTop 0] with n h using if_neg h)] at hS
  have (n) : ‚àë k ‚àà Icc 1 n, (if k = 0 then 0 else f k) = ‚àë k ‚àà Icc 1 n, f k :=
    Finset.sum_congr rfl fun k hk ‚Ü¶ by rw [if_neg (zero_lt_one.trans_le (mem_Icc.mp hk).1).ne']
  rw [‚Üê LSeries_congr _ (fun _ ‚Ü¶ if_neg _), LSeries_eq_mul_integral_aux (if_pos rfl) hr hs hS] <;>
  simp_all

/-- A version of `LSeries_eq_mul_integral` where we use the stronger condition that the partial sums
`‚àë k ‚àà Icc 1 n, ‚Äñf k‚Äñ` are `O(n ^ r)` to deduce the integral representation. -/
theorem LSeries_eq_mul_integral' (f : ‚Ñï ‚Üí ‚ÑÇ) {r : ‚Ñù} (hr : 0 ‚â§ r) {s : ‚ÑÇ} (hs : r < s.re)
    (hO : (fun n ‚Ü¶ ‚àë k ‚àà Icc 1 n, ‚Äñf k‚Äñ) =O[atTop] fun n ‚Ü¶ (n : ‚Ñù) ^ r) :
    LSeries f s = s * ‚à´ t in Set.Ioi (1 : ‚Ñù), (‚àë k ‚àà Icc 1 ‚åät‚åã‚Çä, f k) * t ^ (-(s + 1)) :=
  LSeries_eq_mul_integral _ hr hs (LSeriesSummable_of_sum_norm_bigO hO hr hs) <|
    (isBigO_of_le _ fun _ ‚Ü¶ (norm_sum_le _ _).trans <| Real.le_norm_self _).trans hO

/-- If `f` takes nonnegative real values and the partial sums `‚àë k ‚àà Icc 1 n, f k` are `O(n ^ r)`
for some real `0 ‚â§ r`, then for `s : ‚ÑÇ` with `r < s.re`, we have
`LSeries f s = s * ‚à´ t in Set.Ioi 1, (‚àë k ‚àà Icc 1 ‚åät‚åã‚Çä, f k) * t ^ (-(s + 1))`. -/
theorem LSeries_eq_mul_integral_of_nonneg (f : ‚Ñï ‚Üí ‚Ñù) {r : ‚Ñù} (hr : 0 ‚â§ r) {s : ‚ÑÇ} (hs : r < s.re)
    (hO : (fun n ‚Ü¶ ‚àë k ‚àà Icc 1 n, f k) =O[atTop] fun n ‚Ü¶ (n : ‚Ñù) ^ r) (hf : ‚àÄ n, 0 ‚â§ f n) :
    LSeries (fun n ‚Ü¶ f n) s =
      s * ‚à´ t in Set.Ioi (1 : ‚Ñù), (‚àë k ‚àà Icc 1 ‚åät‚åã‚Çä, (f k : ‚ÑÇ)) * t ^ (-(s + 1)) :=
  LSeries_eq_mul_integral' _ hr hs <| hO.congr_left fun _ ‚Ü¶ by simp [abs_of_nonneg (hf _)]

end integralrepresentation

noncomputable section residue

variable {f : ‚Ñï ‚Üí ‚ÑÇ} {l : ‚ÑÇ}

section lemmas

private theorem lemma‚ÇÅ (hlim : Tendsto (fun n : ‚Ñï ‚Ü¶ (‚àë k ‚àà Icc 1 n, f k) / n) atTop (ùìù l))
    {s : ‚Ñù} (hs : 1 < s) :
    IntegrableOn (fun t : ‚Ñù ‚Ü¶ (‚àë k ‚àà Icc 1 ‚åät‚åã‚Çä, f k) * (t : ‚ÑÇ) ^ (-(s : ‚ÑÇ) - 1)) (Set.Ici 1) := by
  have h‚ÇÅ : LocallyIntegrableOn (fun t : ‚Ñù ‚Ü¶ (‚àë k ‚àà Icc 1 ‚åät‚åã‚Çä, f k) * (t : ‚ÑÇ) ^ (-(s : ‚ÑÇ) - 1))
        (Set.Ici 1) := by
    simp_rw [mul_comm]
    refine locallyIntegrableOn_mul_sum_Icc f zero_le_one ?_
    refine ContinuousOn.locallyIntegrableOn (fun t ht ‚Ü¶ ?_) measurableSet_Ici
    exact (continuousAt_ofReal_cpow_const _ _ <|
      Or.inr (zero_lt_one.trans_le ht).ne').continuousWithinAt
  have h‚ÇÇ : (fun t : ‚Ñù ‚Ü¶ ‚àë k ‚àà Icc 1 ‚åät‚åã‚Çä, f k) =O[atTop] fun t ‚Ü¶ t ^ (1 : ‚Ñù) := by
    simp_rw [Real.rpow_one]
    refine IsBigO.trans_isEquivalent ?_ isEquivalent_nat_floor
    have : Tendsto (fun n ‚Ü¶ (‚àë k ‚àà Icc 1 n, f k) / ((n : ‚Ñù) ^ (1 : ‚Ñù) : ‚Ñù)) atTop (ùìù l) := by
      simpa using hlim
    simpa using (isBigO_atTop_natCast_rpow_of_tendsto_div_rpow this).comp_tendsto
        tendsto_nat_floor_atTop
  refine h‚ÇÅ.integrableOn_of_isBigO_atTop (g := fun t ‚Ü¶ t ^(-s)) ?_ ?_
  ¬∑ refine IsBigO.mul_atTop_rpow_of_isBigO_rpow 1 (-s - 1) _ h‚ÇÇ ?_ (by linarith)
    exact (norm_ofReal_cpow_eventually_eq_atTop _).isBigO.of_norm_left
  ¬∑ rwa [integrableAtFilter_rpow_atTop_iff, neg_lt_neg_iff]

private theorem lemma‚ÇÇ {s T Œµ : ‚Ñù} {S : ‚Ñù ‚Üí ‚ÑÇ} (hs : 1 < s)
    (hS‚ÇÅ : LocallyIntegrableOn (fun t ‚Ü¶ S t) (Set.Ici 1)) (hS‚ÇÇ : ‚àÄ t ‚â• T, ‚ÄñS t‚Äñ ‚â§ Œµ * t) :
    IntegrableOn (fun t : ‚Ñù ‚Ü¶ ‚ÄñS t‚Äñ * (t ^ (-s - 1))) (Set.Ici 1) := by
  have h : LocallyIntegrableOn (fun t : ‚Ñù ‚Ü¶ ‚ÄñS t‚Äñ * (t ^ (-s - 1))) (Set.Ici 1) := by
    refine hS‚ÇÅ.norm.mul_continuousOn ?_ isLocallyClosed_Ici
    exact fun t ht ‚Ü¶ (Real.continuousAt_rpow_const _ _
      <| Or.inl (zero_lt_one.trans_le ht).ne').continuousWithinAt
  refine h.integrableOn_of_isBigO_atTop (g := fun t ‚Ü¶ t ^(-s)) (isBigO_iff.mpr ‚ü®Œµ, ?_‚ü©) ?_
  ¬∑ filter_upwards [eventually_ge_atTop T, eventually_gt_atTop 0] with t ht ht'
    simpa [abs_of_nonneg, Real.rpow_nonneg, ht'.le, Real.rpow_sub ht', mul_assoc, ht'.ne',
      mul_div_cancel‚ÇÄ] using mul_le_mul_of_nonneg_right (hS‚ÇÇ t ht) (norm_nonneg <| t ^ (-s - 1))
  ¬∑ exact integrableAtFilter_rpow_atTop_iff.mpr <| neg_lt_neg_iff.mpr hs

end lemmas

section proof
-- See `LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div_aux‚ÇÉ` for the strategy of proof

private theorem LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div_aux‚ÇÅ
  (hlim : Tendsto (fun n : ‚Ñï ‚Ü¶ (‚àë k ‚àà Icc 1 n, f k) / n) atTop (ùìù l)) {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) :
    ‚àÄ·∂† t : ‚Ñù in atTop, ‚Äñ(‚àë k ‚àà Icc 1 ‚åät‚åã‚Çä, f k) - l * t‚Äñ < Œµ * t := by
  have h_lim' : Tendsto (fun t : ‚Ñù ‚Ü¶ (‚àë k ‚àà Icc 1 ‚åät‚åã‚Çä, f k : ‚ÑÇ) / t) atTop (ùìù l) := by
    refine (mul_one l ‚ñ∏ ofReal_one ‚ñ∏ ((hlim.comp tendsto_nat_floor_atTop).mul <|
      tendsto_ofReal_iff.mpr <| tendsto_nat_floor_div_atTop)).congr' ?_
    filter_upwards [eventually_ge_atTop 1] with t ht
    simp [div_mul_div_cancel‚ÇÄ (show (‚åät‚åã‚Çä : ‚ÑÇ) ‚â† 0 by simpa)]
  filter_upwards [eventually_gt_atTop 0, Metric.tendsto_nhds.mp h_lim' Œµ hŒµ] with t ht‚ÇÅ ht‚ÇÇ
  rwa [dist_eq_norm, div_sub' (ne_zero_of_re_pos ht‚ÇÅ), norm_div, norm_real,
    Real.norm_of_nonneg ht‚ÇÅ.le, mul_comm, div_lt_iff‚ÇÄ ht‚ÇÅ] at ht‚ÇÇ

private theorem LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div_aux‚ÇÇ {s T Œµ : ‚Ñù} {S : ‚Ñù ‚Üí ‚ÑÇ}
    (hS : LocallyIntegrableOn (fun t ‚Ü¶ S t - l * t) (Set.Ici 1)) (hŒµ : 0 < Œµ)
    (hs : 1 < s) (hT‚ÇÅ : 1 ‚â§ T) (hT : ‚àÄ t ‚â• T, ‚ÄñS t - l * t‚Äñ ‚â§ Œµ * t) :
    (s - 1) * ‚à´ (t : ‚Ñù) in Set.Ioi T, ‚ÄñS t - l * t‚Äñ * t ^ (-s - 1) ‚â§ Œµ := by
  have hT‚ÇÄ : 0 < T := zero_lt_one.trans_le hT‚ÇÅ
  have h {t : ‚Ñù} (ht : 0 < t) : t ^ (-s) = t * t ^ (-s - 1) := by
    rw [Real.rpow_sub ht, Real.rpow_one, mul_div_cancel‚ÇÄ _ ht.ne']
  calc
    _ ‚â§ (s - 1) * ‚à´ (t : ‚Ñù) in Set.Ioi T, Œµ * t ^ (-s) := by
      refine mul_le_mul_of_nonneg_left (setIntegral_mono_on ?_ ?_ measurableSet_Ioi fun t ht ‚Ü¶ ?_)
        (sub_pos_of_lt hs).le
      ¬∑ exact (lemma‚ÇÇ hs hS hT).mono_set <| Set.Ioi_subset_Ici_iff.mpr hT‚ÇÅ
      ¬∑ exact (integrableOn_Ioi_rpow_of_lt (neg_lt_neg_iff.mpr hs) hT‚ÇÄ).const_mul  _
      ¬∑ have ht' : 0 < t := hT‚ÇÄ.trans ht
        rw [h ht', ‚Üê mul_assoc]
        exact mul_le_mul_of_nonneg_right (hT t ht.le) (Real.rpow_nonneg ht'.le _)
    _ ‚â§ Œµ * ((s - 1) * ‚à´ (t : ‚Ñù) in Set.Ioi 1, t ^ (-s)) := by
      rw [integral_const_mul, ‚Üê mul_assoc, ‚Üê mul_assoc, mul_comm Œµ]
      refine mul_le_mul_of_nonneg_left (setIntegral_mono_set ?_ ?_
        (Set.Ioi_subset_Ioi hT‚ÇÅ).eventuallyLE) (mul_nonneg (sub_pos_of_lt hs).le hŒµ.le)
      ¬∑ exact integrableOn_Ioi_rpow_of_lt (neg_lt_neg_iff.mpr hs) zero_lt_one
      ¬∑ exact (ae_restrict_iff' measurableSet_Ioi).mpr <| univ_mem' fun t ht ‚Ü¶
        Real.rpow_nonneg (zero_le_one.trans ht.le) _
    _ = Œµ := by
      rw [integral_Ioi_rpow_of_lt (by rwa [neg_lt_neg_iff]) zero_lt_one, Real.one_rpow]
      field_simp [show -s + 1 ‚â† 0 by linarith, hŒµ.ne']
      ring

private theorem LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div_aux‚ÇÉ
    (hlim : Tendsto (fun n : ‚Ñï ‚Ü¶ (‚àë k ‚àà Icc 1 n, f k) / n) atTop (ùìù l))
    (hfS : ‚àÄ s : ‚Ñù, 1 < s ‚Üí LSeriesSummable f s) {Œµ : ‚Ñù} (hŒµ : Œµ > 0) :
    ‚àÉ C ‚â• 0, (fun s : ‚Ñù ‚Ü¶ ‚Äñ(s - 1) * LSeries f s - s * l‚Äñ) ‚â§·∂†[ùìù[>] 1]
      fun s ‚Ü¶ (s - 1) * s * C + s * Œµ := by
  obtain ‚ü®T, hT‚ÇÅ, hT‚ü© := (eventually_forall_ge_atTop.mpr
    (LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div_aux‚ÇÅ
      hlim hŒµ)).frequently.forall_exists_of_atTop 1
  let S : ‚Ñù ‚Üí ‚ÑÇ := fun t ‚Ü¶ ‚àë k ‚àà Icc 1 ‚åät‚åã‚Çä, f k
  let C := ‚à´ t in Set.Ioc 1 T, ‚ÄñS t - l * t‚Äñ * t ^ (-1 - 1 : ‚Ñù)
  have hC : 0 ‚â§ C := by
    refine setIntegral_nonneg_ae measurableSet_Ioc (univ_mem' fun t ht ‚Ü¶ ?_)
    exact mul_nonneg (norm_nonneg _) <| Real.rpow_nonneg (zero_le_one.trans ht.1.le) _
  refine ‚ü®C, hC, ?_‚ü©
  filter_upwards [eventually_mem_nhdsWithin] with s hs
  rw [Set.mem_Ioi] at hs
  have hs' : 0 ‚â§ (s - 1) * s := mul_nonneg (sub_nonneg.mpr hs.le) (zero_le_one.trans hs.le)
  have h‚ÇÄ : LocallyIntegrableOn (fun t ‚Ü¶ S t - l * t) (Set.Ici 1) := by
    refine .sub ?_ <| ContinuousOn.locallyIntegrableOn (by fun_prop) measurableSet_Ici
    simpa using locallyIntegrableOn_mul_sum_Icc f zero_le_one (locallyIntegrableOn_const 1)
  have h‚ÇÅ : IntegrableOn (fun t ‚Ü¶ ‚ÄñS t - l * t‚Äñ * t ^ (-s - 1)) (Set.Ici 1) :=
    lemma‚ÇÇ hs h‚ÇÄ fun t ht ‚Ü¶ (hT t ht).le
  have h‚ÇÇ : IntegrableOn (fun t : ‚Ñù ‚Ü¶ ‚ÄñS t - l * t‚Äñ * (t ^ ((-1 : ‚Ñù) - 1))) (Set.Ioc 1 T) := by
    refine ((h‚ÇÄ.norm.mul_continuousOn ?_ isLocallyClosed_Ici).integrableOn_compact_subset
      Set.Icc_subset_Ici_self isCompact_Icc).mono_set Set.Ioc_subset_Icc_self
    exact fun t ht ‚Ü¶ (Real.continuousAt_rpow_const _ _
      <| Or.inl (zero_lt_one.trans_le ht).ne').continuousWithinAt
  have h‚ÇÉ : (s - 1) * ‚à´ (t : ‚Ñù) in Set.Ioi 1, (t : ‚ÑÇ) ^ (-s : ‚ÑÇ) = 1 := by
    rw [integral_Ioi_cpow_of_lt (by rwa [neg_re, neg_lt_neg_iff]) zero_lt_one, ofReal_one,
      one_cpow, show -(s : ‚ÑÇ) + 1 = -(s - 1) by ring, neg_div_neg_eq, mul_div_cancel‚ÇÄ]
    exact (sub_ne_zero.trans ofReal_ne_one).mpr hs.ne'
  let Cs := ‚à´ t in Set.Ioc 1 T, ‚ÄñS t - l * t‚Äñ * t ^ (-s - 1)
  have h‚ÇÑ : Cs ‚â§ C := by
    refine setIntegral_mono_on ?_ h‚ÇÇ measurableSet_Ioc fun t ht ‚Ü¶ ?_
    ¬∑ exact h‚ÇÅ.mono_set <| Set.Ioc_subset_Ioi_self.trans Set.Ioi_subset_Ici_self
    ¬∑ gcongr
      exact ht.1.le
  calc
    -- First, we replace `s * l` by `(s - 1) * s` times the integral of `l * t ^ (-s)` using `h‚ÇÉ`
    -- and replace `LSeries f s` by its integral representation.
    _ = ‚Äñ((s - 1) * s * ‚à´ t in Set.Ioi 1, S t * ‚Üët ^ (-(s : ‚ÑÇ) - 1)) -
          l * s * ((s - 1) * ‚à´ (t : ‚Ñù) in Set.Ioi 1, ‚Üët ^ (-(s : ‚ÑÇ)))‚Äñ := by
      rw [h‚ÇÉ, mul_one, mul_comm l, LSeries_eq_mul_integral _ zero_le_one (by rwa [ofReal_re])
        (hfS _ hs), neg_add', mul_assoc]
      exact isBigO_atTop_natCast_rpow_of_tendsto_div_rpow (a := l) (by simpa using hlim)
    _ = ‚Äñ(s - 1) * s * ‚à´ t in Set.Ioi 1, (S t * (t : ‚ÑÇ) ^ (-s - 1 : ‚ÑÇ) - l * t ^ (-s : ‚ÑÇ))‚Äñ := by
      rw [integral_sub, integral_const_mul]
      ¬∑ congr; ring
      ¬∑ exact (lemma‚ÇÅ hlim hs).mono_set Set.Ioi_subset_Ici_self
      ¬∑ exact (integrableOn_Ioi_cpow_of_lt
          (by rwa [neg_re, ofReal_re, neg_lt_neg_iff]) zero_lt_one).const_mul  _
    _ = ‚Äñ(s - 1) * s * ‚à´ t in Set.Ioi 1, (S t - l * t) * (t : ‚ÑÇ) ^ (-s - 1 : ‚ÑÇ)‚Äñ := by
      congr 2
      refine setIntegral_congr_fun measurableSet_Ioi fun t ht ‚Ü¶ ?_
      replace ht : (t : ‚ÑÇ) ‚â† 0 := ne_zero_of_one_lt_re ht
      rw [sub_mul, cpow_sub _ _ ht, cpow_one, mul_assoc, mul_div_cancel‚ÇÄ _ ht]
    _ ‚â§ (s - 1) * s * ‚à´ t in Set.Ioi 1, ‚Äñ(S t - l * ‚Üët) * ‚Üët ^ (-s - 1 : ‚ÑÇ)‚Äñ := by
      rw [norm_mul, show ((s : ‚ÑÇ) - 1) * s = ((s - 1) * s : ‚Ñù) by simp, norm_real,
        Real.norm_of_nonneg hs']
      exact mul_le_mul_of_nonneg_left (norm_integral_le_integral_norm _) hs'
    -- Next, step is to bound the integral of `‚ÄñS t - l * t‚Äñ * t ^ (-s - 1)`.
    _ = (s - 1) * s * ‚à´ t in Set.Ioi 1, ‚ÄñS t - l * t‚Äñ * t ^ (-s - 1) := by
      congr 1
      refine setIntegral_congr_fun measurableSet_Ioi fun t ht ‚Ü¶ ?_
      replace ht : 0 ‚â§ t := zero_le_one.trans ht.le
      rw [norm_mul, show (-(s : ‚ÑÇ) - 1) = (-s - 1 : ‚Ñù) by simp, ‚Üê ofReal_cpow ht, norm_real,
        Real.norm_of_nonneg (Real.rpow_nonneg ht _)]
    -- For that, we cut the integral in two parts using `T` as the cutting point.
    _ = (s - 1) * s * (Cs + ‚à´ t in Set.Ioi T, ‚ÄñS t - l * t‚Äñ * t ^ (-s - 1)) := by
      rw [‚Üê Set.Ioc_union_Ioi_eq_Ioi hT‚ÇÅ, setIntegral_union Set.Ioc_disjoint_Ioi_same
        measurableSet_Ioi]
      ¬∑ exact h‚ÇÅ.mono_set <| Set.Ioc_subset_Ioi_self.trans Set.Ioi_subset_Ici_self
      ¬∑ exact h‚ÇÅ.mono_set <| Set.Ioi_subset_Ici_self.trans <| Set.Ici_subset_Ici.mpr hT‚ÇÅ
    -- The first part can be bounded by `C` using `h‚ÇÑ`.
    _ ‚â§ (s - 1) * s * C + s * ((s - 1) * ‚à´ t in Set.Ioi T, ‚ÄñS t - l * t‚Äñ * t ^ (-s - 1)) := by
      rw [mul_add, ‚Üê mul_assoc, mul_comm s]
      gcongr
    -- The second part is bounded using `LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div_aux‚ÇÇ`
    -- since `‚ÄñS t - l t‚Äñ ‚â§ Œµ * t` for all `t ‚â• T`.
    _ ‚â§ (s - 1) * s * C + s * Œµ := by
      gcongr
      exact LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div_aux‚ÇÇ h‚ÇÄ hŒµ hs hT‚ÇÅ
        fun t ht ‚Ü¶ (hT t ht.le).le

theorem LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div
    (hlim : Tendsto (fun n : ‚Ñï ‚Ü¶ (‚àë k ‚àà Icc 1 n, f k) / n) atTop (ùìù l))
    (hfS : ‚àÄ s : ‚Ñù, 1 < s ‚Üí LSeriesSummable f s) :
    Tendsto (fun s : ‚Ñù ‚Ü¶ (s - 1) * LSeries f s) (ùìù[>] 1) (ùìù l) := by
  have h‚ÇÅ {C Œµ : ‚Ñù} : Tendsto (fun s ‚Ü¶ (s - 1) * s * C + s * Œµ) (ùìù[>] 1) (ùìù Œµ) := by
    rw [show ùìù Œµ = ùìù ((1 - 1) * 1 * C + 1 * Œµ) by congr; ring]
    exact tendsto_nhdsWithin_of_tendsto_nhds (ContinuousAt.tendsto (by fun_prop))
  have h‚ÇÇ : IsBoundedUnder
      (fun x1 x2 ‚Ü¶ x1 ‚â§ x2) (ùìù[>] 1) fun s : ‚Ñù ‚Ü¶ ‚Äñ(s - 1) * LSeries f s - s * l‚Äñ := by
    obtain ‚ü®C, _, hC‚ÇÇ‚ü© :=
      LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div_aux‚ÇÉ hlim hfS zero_lt_one
    exact h‚ÇÅ.isBoundedUnder_le.mono_le hC‚ÇÇ
  suffices Tendsto (fun s : ‚Ñù ‚Ü¶ (s - 1) * LSeries f s - s * l) (ùìù[>] 1) (ùìù 0) by
    rw [show ùìù l = ùìù (0 + 1 * l) by congr; ring]
    have h‚ÇÉ : Tendsto (fun s : ‚Ñù ‚Ü¶ s * l) (ùìù[>] 1) (ùìù (1 * l)) :=
      tendsto_nhdsWithin_of_tendsto_nhds (ContinuousAt.tendsto (by fun_prop))
    exact (this.add h‚ÇÉ).congr fun _ ‚Ü¶ by ring
  refine tendsto_zero_iff_norm_tendsto_zero.mpr <| tendsto_of_le_liminf_of_limsup_le ?_ ?_ h‚ÇÇ ?_
  ¬∑ exact le_liminf_of_le h‚ÇÇ.isCoboundedUnder_ge (univ_mem' (fun _ ‚Ü¶ norm_nonneg _))
  ¬∑ refine le_of_forall_pos_le_add fun Œµ hŒµ ‚Ü¶ ?_
    rw [zero_add]
    obtain ‚ü®C, hC‚ÇÅ, hC‚ÇÇ‚ü© := LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div_aux‚ÇÉ hlim hfS hŒµ
    refine le_of_le_of_eq (limsup_le_limsup hC‚ÇÇ ?_ h‚ÇÅ.isBoundedUnder_le) h‚ÇÅ.limsup_eq
    exact isCoboundedUnder_le_of_eventually_le _ (univ_mem' fun _ ‚Ü¶ norm_nonneg _)
  ¬∑ exact isBoundedUnder_of_eventually_ge (univ_mem' fun _ ‚Ü¶ norm_nonneg _)

theorem LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div_and_nonneg (f : ‚Ñï ‚Üí ‚Ñù) {l : ‚Ñù}
    (hf : Tendsto (fun n ‚Ü¶ (‚àë k ‚àà Icc 1 n, f k) / (n : ‚Ñù)) atTop (ùìù l))
    (hf' : ‚àÄ n, 0 ‚â§ f n) :
    Tendsto (fun s : ‚Ñù ‚Ü¶ (s - 1) * LSeries (fun n ‚Ü¶ f n) s) (ùìù[>] 1) (ùìù l) := by
  refine  LSeries_tendsto_sub_mul_nhds_one_of_tendsto_sum_div (f := fun n ‚Ü¶ f n)
    (hf.ofReal.congr fun _ ‚Ü¶ ?_) fun s hs ‚Ü¶ ?_
  ¬∑ simp
  ¬∑ refine LSeriesSummable_of_sum_norm_bigO_and_nonneg ?_ hf' zero_le_one hs
    exact isBigO_atTop_natCast_rpow_of_tendsto_div_rpow (by simpa)

end proof

end residue
