/-
Copyright (c) 2025 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.Analysis.Asymptotics.SpecificAsymptotics
import Mathlib.Analysis.InnerProductSpace.Calculus
import Mathlib.Analysis.SpecialFunctions.ImproperIntegrals
import Mathlib.NumberTheory.AbelSummation
import Mathlib.NumberTheory.LSeries.Basic

/-!
# Partial sums of coefficients of L-series

We prove several results involving partial sums of coefficients (or norm of coefficients) of
L-series.

## Main results

* `LSeriesSummable_of_sum_norm_bigO`: for `f : â„• â†’ â„‚`, if the partial sums
  `âˆ‘ k âˆˆ Icc 1 n, â€–f kâ€–` are `O(n ^ r)` for some real `0 â‰¤ r`, then the L-series `LSeries f`
  converges at `s : â„‚` for all `s` such that `r < s.re`.

* `LSeries_eq_mul_integral` : for `f : â„• â†’ â„‚`, if the partial sums `âˆ‘ k âˆˆ Icc 1 n, f k` are
  `O(n ^ r)` for some real `0 â‰¤ r` and the L-series `LSeries f` converges at `s : â„‚` with
  `r < s.re`, then `LSeries f s = s * âˆ« t in Set.Ioi 1, (âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, f k) * t ^ (- (s + 1))`.

-/

open Finset Filter MeasureTheory Topology Complex Asymptotics

section summable

variable {f : â„• â†’ â„‚} {r : â„} {s : â„‚}

private theorem LSeriesSummable_of_sum_norm_bigO_aux (hf : f 0 = 0)
    (hO : (fun n â†¦ âˆ‘ k âˆˆ Icc 1 n, â€–f kâ€–) =O[atTop] fun n â†¦ (n : â„) ^ r)
    (hr : 0 â‰¤ r) (hs : r < s.re) :
     LSeriesSummable f s := by
  have hâ‚ : -s â‰  0 := neg_ne_zero.mpr <| ne_zero_of_re_pos (hr.trans_lt hs)
  have hâ‚‚ : (-s).re + r â‰¤ 0 := by
    rw [neg_re, neg_add_nonpos_iff]
    exact hs.le
  have hâ‚ƒ : âˆ€ t âˆˆ Set.Ici (1 : â„), DifferentiableAt â„ (fun x : â„ â†¦ â€–(x : â„‚) ^ (-s)â€–) t := by
    intro t ht
    have ht' : t â‰  0 := (zero_lt_one.trans_le ht).ne'
    exact (differentiableAt_id.ofReal_cpow_const ht' hâ‚).norm â„ <|
      (cpow_ne_zero_iff_of_exponent_ne_zero hâ‚).mpr <| ofReal_ne_zero.mpr ht'
  have hâ‚„ : (deriv fun t : â„ â†¦ â€–(t : â„‚) ^ (- s)â€–) =á¶ [atTop] fun t â†¦ - s.re * t ^ (- (s.re +1)) := by
    filter_upwards [eventually_gt_atTop 0] with t ht
    rw [deriv_norm_ofReal_cpow _ ht, neg_re, neg_add']
  change Summable (fun n â†¦ LSeries.term f s n)
  simp_rw [LSeries.term_defâ‚€ hf, mul_comm (f _)]
  refine summable_mul_of_bigO_atTop' (f := fun t â†¦ (t : â„‚) ^ (-s))
    (g := fun t â†¦ t ^ (- (s.re + 1) + r)) _ hâ‚ƒ ?_ ?_ ?_ ?_
  Â· refine integrableOn_Ici_iff_integrableOn_Ioi.mpr
      (integrableOn_Ioi_deriv_norm_ofReal_cpow zero_lt_one ?_)
    exact neg_re _ â–¸ neg_nonpos.mpr  <| hr.trans hs.le
  Â· refine (IsBigO.mul_atTop_rpow_natCast_of_isBigO_rpow ((- s).re) r 0 ?_ hO hâ‚‚).congr_right
      (by simp)
    exact (norm_ofReal_cpow_eventually_eq_atTop _).isBigO.natCast_atTop
  Â· refine IsBigO.mul_atTop_rpow_of_isBigO_rpow (- (s.re + 1)) r _ ?_ ?_ le_rfl
    Â· exact (EventuallyEq.isBigO hâ‚„).of_const_mul_right
    Â· exact (hO.comp_tendsto tendsto_nat_floor_atTop).trans <|
        isEquivalent_nat_floor.isBigO.rpow hr (eventually_ge_atTop 0)
  Â· exact integrableAtFilter_rpow_atTop_iff.mpr
      (by rwa [neg_add_lt_iff_lt_add, add_neg_cancel_right])

/-- If the partial sums `âˆ‘ k âˆˆ Icc 1 n, â€–f kâ€–` are `O(n ^ r)` for some real `0 â‰¤ r`, then the
L-series `LSeries f` converges at `s : â„‚` for all `s` such that `r < s.re`. -/
theorem LSeriesSummable_of_sum_norm_bigO
    (hO : (fun n â†¦ âˆ‘ k âˆˆ Icc 1 n, â€–f kâ€–) =O[atTop] fun n â†¦ (n : â„) ^ r)
    (hr : 0 â‰¤ r) (hs : r < s.re) :
    LSeriesSummable f s := by
  have hâ‚ : (fun n â†¦ if n = 0 then 0 else f n) =á¶ [atTop] f := by
    filter_upwards [eventually_ne_atTop 0] with n hn using by simp_rw [if_neg hn]
  refine LSeriesSummable.congr' _ hâ‚ ?_
  refine LSeriesSummable_of_sum_norm_bigO_aux (by rw [if_pos rfl]) ?_ hr hs
  refine hO.congr' (Eventually.of_forall fun _ â†¦ Finset.sum_congr rfl fun _ h â†¦ ?_) EventuallyEq.rfl
  rw [if_neg (zero_lt_one.trans_le (mem_Icc.mp h).1).ne']

/-- If `f` takes nonnegative real values and the partial sums `âˆ‘ k âˆˆ Icc 1 n, f k` are `O(n ^ r)`
for some real `0 â‰¤ r`, then the L-series `LSeries f` converges at `s : â„‚` for all `s`
such that `r < s.re`. -/
theorem LSeriesSummable_of_sum_norm_bigO_and_nonneg
    {f : â„• â†’ â„} (hO : (fun n â†¦ âˆ‘ k âˆˆ Icc 1 n, f k) =O[atTop] fun n â†¦ (n : â„) ^ r)
    (hf : âˆ€ n, 0 â‰¤ f n) (hr : 0 â‰¤ r) (hs : r < s.re) :
    LSeriesSummable (fun n â†¦ f n) s := by
  refine LSeriesSummable_of_sum_norm_bigO ?_ hr hs
  simp_rw [norm_real, Real.norm_of_nonneg (hf _)]
  exact hO

end summable

section integralrepresentation

private theorem LSeries_eq_mul_integral_aux {f : â„• â†’ â„‚} (hf : f 0 = 0) {r : â„} (hr : 0 â‰¤ r) {s : â„‚}
    (hs : r < s.re) (hS : LSeriesSummable f s)
    (hO : (fun n â†¦ âˆ‘ k âˆˆ Icc 1 n, f k) =O[atTop] fun n â†¦ (n : â„) ^ r) :
    LSeries f s = s * âˆ« t in Set.Ioi (1 : â„), (âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, f k) * t ^ (- (s + 1)) := by
  have hâ‚ : (-s - 1).re + r < -1 := by
    rwa [sub_re, one_re, neg_re, neg_sub_left, neg_add_lt_iff_lt_add, add_neg_cancel_comm]
  have hâ‚‚ : s â‰  0 := ne_zero_of_re_pos (hr.trans_lt hs)
  have hâ‚ƒ : âˆ€ t âˆˆ Set.Ici (1 : â„), DifferentiableAt â„ (fun x : â„ â†¦ (x : â„‚) ^ (-s)) t :=
    fun t ht â†¦ differentiableAt_id.ofReal_cpow_const (zero_lt_one.trans_le ht).ne'
      (neg_ne_zero.mpr hâ‚‚)
  have hâ‚„ : âˆ€ n, âˆ‘ k âˆˆ Icc 0 n, f k = âˆ‘ k âˆˆ Icc 1 n, f k := fun n â†¦ by
    rw [â† Nat.Icc_insert_succ_left n.zero_le, sum_insert (by aesop), hf, zero_add, zero_add]
  simp_rw [â† hâ‚„] at hO
  rw [â† integral_mul_left]
  refine tendsto_nhds_unique ((tendsto_add_atTop_iff_nat 1).mpr hS.hasSum.tendsto_sum_nat) ?_
  simp_rw [Nat.range_succ_eq_Icc_zero, LSeries.term_defâ‚€ hf, mul_comm (f _)]
  convert tendsto_sum_mul_atTop_nhds_one_sub_integralâ‚€ (f := fun x â†¦ (x : â„‚) ^ (-s)) (l := 0)
    ?_ hf hâ‚ƒ ?_ ?_ ?_ (integrableAtFilter_rpow_atTop_iff.mpr hâ‚)
  Â· rw [zero_sub, â† integral_neg]
    refine setIntegral_congr_fun measurableSet_Ioi fun t ht â†¦ ?_
    rw [deriv_ofReal_cpow_const (zero_lt_one.trans ht).ne', hâ‚„]
    Â· ring_nf
    Â· exact neg_ne_zero.mpr <| ne_zero_of_re_pos (hr.trans_lt hs)
  Â· refine integrableOn_Ici_iff_integrableOn_Ioi.mpr <|
      integrableOn_Ioi_deriv_ofReal_cpow zero_lt_one ?_
    rw [neg_re, neg_lt_zero]
    exact hr.trans_lt hs
  Â· have hlim : Tendsto (fun n : â„• â†¦ (n : â„) ^ (- (s.re - r))) atTop (ð“ 0) :=
      (tendsto_rpow_neg_atTop (by rwa [sub_pos])).comp tendsto_natCast_atTop_atTop
    refine IsBigO.trans_tendsto ?_ hlim
    refine IsBigO.mul_atTop_rpow_natCast_of_isBigO_rpow (- s.re) _ _ ?_ hO ?_
    Â· exact isBigO_norm_left.mp <| (norm_ofReal_cpow_eventually_eq_atTop _).isBigO.natCast_atTop
    Â· rw [neg_sub', sub_neg_eq_add]
  Â· refine IsBigO.mul_atTop_rpow_of_isBigO_rpow (- (s + 1).re) r _ ?_ ?_
      (by rw [â† neg_re, neg_add'])
    Â· rw [add_re, one_re, neg_add']
      exact isBigO_deriv_ofReal_cpow_const_atTop _
    Â· exact (hO.comp_tendsto tendsto_nat_floor_atTop).trans <|
        isEquivalent_nat_floor.isBigO.rpow hr (eventually_ge_atTop 0)

/-- If the partial sums `âˆ‘ k âˆˆ Icc 1 n, f k` are `O(n ^ r)` for some real `0 â‰¤ r` and the
L-series `LSeries f` converges at `s : â„‚` with `r < s.re`, then
`LSeries f s = s * âˆ« t in Set.Ioi 1, (âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, f k) * t ^ (- (s + 1))`. -/
theorem LSeries_eq_mul_integral (f : â„• â†’ â„‚) {r : â„} (hr : 0 â‰¤ r) {s : â„‚} (hs : r < s.re)
    (hS : LSeriesSummable f s)
    (hO : (fun n â†¦ âˆ‘ k âˆˆ Icc 1 n, f k) =O[atTop] fun n â†¦ (n : â„) ^ r) :
    LSeries f s = s * âˆ« t in Set.Ioi (1 : â„), (âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, f k) * t ^ (- (s + 1)) := by
  have hâ‚ : (fun n â†¦ if n = 0 then 0 else f n) =á¶ [atTop] f := by
    filter_upwards [eventually_ne_atTop 0] with n hn using by simp_rw [if_neg hn]
  have hâ‚‚ := (LSeriesSummable_congr' s hâ‚).mpr hS
  have hâ‚ƒ : âˆ€ {n}, n â‰  0 â†’ (fun n â†¦ if n = 0 then 0 else f n) n = f n :=
    fun hn â†¦ by simp_rw [if_neg hn]
  have hâ‚„ : âˆ€ n, âˆ‘ k âˆˆ Icc 1 n, (if k = 0 then 0 else f k) = âˆ‘ k âˆˆ Icc 1 n, f k := fun n â†¦
    Finset.sum_congr rfl fun k hk â†¦ by rw [if_neg (zero_lt_one.trans_le (mem_Icc.mp hk).1).ne']
  rw [â† LSeries_congr _ hâ‚ƒ, LSeries_eq_mul_integral_aux (by rw [if_pos rfl]) hr hs hâ‚‚ ?_]
  Â· simp_rw [hâ‚„]
  Â· simpa [hâ‚„] using hO

/-- A version of `LSeries_eq_mul_integral` where we use the stronger condition that the partial sums
`âˆ‘ k âˆˆ Icc 1 n, â€–f kâ€–` are `O(n ^ r)` to deduce the integral representation. -/
theorem LSeries_eq_mul_integral' (f : â„• â†’ â„‚) {r : â„} (hr : 0 â‰¤ r) {s : â„‚} (hs : r < s.re)
    (hO : (fun n â†¦ âˆ‘ k âˆˆ Icc 1 n, â€–f kâ€–) =O[atTop] fun n â†¦ (n : â„) ^ r) :
    LSeries f s = s * âˆ« t in Set.Ioi (1 : â„), (âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, f k) * t ^ (- (s + 1)) := by
  refine LSeries_eq_mul_integral _ hr hs (LSeriesSummable_of_sum_norm_bigO hO hr hs) ?_
  exact IsBigO.trans (isBigO_of_le _ fun _ â†¦ (norm_sum_le _ _).trans <| Real.le_norm_self _) hO

/-- If `f` takes nonnegative real values and the partial sums `âˆ‘ k âˆˆ Icc 1 n, f k` are `O(n ^ r)`
for some real `0 â‰¤ r`, then for `s : â„‚` with `r < s.re`, we have
`LSeries f s = s * âˆ« t in Set.Ioi 1, (âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, f k) * t ^ (- (s + 1))`. -/
theorem LSeries_eq_mul_integral_of_nonneg (f : â„• â†’ â„) {r : â„} (hr : 0 â‰¤ r) {s : â„‚} (hs : r < s.re)
    (hO : (fun n â†¦ âˆ‘ k âˆˆ Icc 1 n, f k) =O[atTop] fun n â†¦ (n : â„) ^ r) (hf : âˆ€ n, 0 â‰¤ f n) :
    LSeries (fun n â†¦ f n) s =
      s * âˆ« t in Set.Ioi (1 : â„), (âˆ‘ k âˆˆ Icc 1 âŒŠtâŒ‹â‚Š, (f k : â„‚)) * t ^ (- (s + 1)) :=
  LSeries_eq_mul_integral' _ hr hs <| hO.congr_left fun _ â†¦ by
    simp_rw [norm_real, Real.norm_of_nonneg (hf _)]

end integralrepresentation
