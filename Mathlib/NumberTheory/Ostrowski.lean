/-
Copyright (c) 2024 Mar√≠a In√©s de Frutos-Fern√°ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Kurniadi Angdinata, Fabrizio Barroero, Laura Capuano, Nirvana Coppola,
Mar√≠a In√©s de Frutos-Fern√°ndez, Sam van Gool, Silvain Rideau-Kikuchi, Amos Turchet,
Francesco Veneziano
-/

import Mathlib.Analysis.SpecialFunctions.Log.Base
import Mathlib.Analysis.SpecialFunctions.Pow.Continuity
import Mathlib.NumberTheory.Padics.PadicNorm

/-!
# Ostrowski‚Äôs Theorem

Ostrowski's Theorem for the field `‚Ñö`: every absolute value on `‚Ñö` is equivalent to either a
`p`-adic absolute value or to the standard Archimedean (Euclidean) absolute value.

## Main results

- `Rat.AbsoluteValue.equiv_real_or_padic`: given an absolute value on `‚Ñö`, it is equivalent
to the standard Archimedean (Euclidean) absolute value `Rat.AbsoluteValue.real` or to a `p`-adic
absolute value `Rat.AbsoluteValue.padic p` for some prime number `p`.

## TODO

Extend to arbitrary number fields.

## References

* [K. Conrad, *Ostrowski's Theorem for Q*][conradQ]
* [K. Conrad, *Ostrowski for number fields*][conradnumbfield]
* [J. W. S. Cassels, *Local fields*][cassels1986local]

## Tags

ring norm, ostrowski
-/

/-!
### API for AbsoluteValue (from MulRingNorm)
-/

section API

namespace AbsoluteValue

variable {R : Type*} [Semiring R]

/-- Triangle inequality for `AbsoluteValue` applied to a list. -/
lemma listSum_le {S : Type*} [OrderedSemiring S] (l : List R) (f : AbsoluteValue R S) :
    f l.sum ‚â§ (l.map f).sum := by
  induction l with
  | nil => simp
  | cons head tail ih => exact (f.add_le' ..).trans <| add_le_add_left ih (f head)

/-- Two absolute values `f, g` on `R` with values in `‚Ñù` are *equivalent* if there exists
a positive constant `c` such that for all `x ‚àà R`, `(f x)^c = g x`. -/
def equiv (f g : AbsoluteValue R ‚Ñù) :=
  ‚àÉ c : ‚Ñù, 0 < c ‚àß (f ¬∑ ^ c) = g

/-- Equivalence of absolute values is reflexive. -/
lemma equiv_refl (f : AbsoluteValue R ‚Ñù) : equiv f f :=
  ‚ü®1, Real.zero_lt_one, funext fun x ‚Ü¶ Real.rpow_one (f x)‚ü©

/-- Equivalence of absolute values is symmetric. -/
lemma equiv_symm {f g : AbsoluteValue R ‚Ñù} (hfg : equiv f g) : equiv g f := by
  rcases hfg with ‚ü®c, hcpos, h‚ü©
  refine ‚ü®1 / c, one_div_pos.mpr hcpos, ?_‚ü©
  simp [‚Üê h, Real.rpow_rpow_inv (apply_nonneg f _) (ne_of_lt hcpos).symm]

/-- Equivalence of absolute values is transitive. -/
lemma equiv_trans {f g k : AbsoluteValue R ‚Ñù} (hfg : equiv f g) (hgk : equiv g k) :
    equiv f k := by
  rcases hfg with ‚ü®c, hcPos, hfg‚ü©
  rcases hgk with ‚ü®d, hdPos, hgk‚ü©
  refine ‚ü®c * d, mul_pos hcPos hdPos, ?_‚ü©
  simp [‚Üê hgk, ‚Üê hfg, Real.rpow_mul (apply_nonneg f _)]

/-- The *trivial* absolute value takes the value `1` on all nonzero elements. -/
protected
def trivial [DecidablePred fun x : R ‚Ü¶ x = 0] [NoZeroDivisors R] {S : Type*} [OrderedSemiring S]
    [Nontrivial S] :
    AbsoluteValue R S where
  toFun x := if x = 0 then 0 else 1
  map_mul' x y := by
    rcases eq_or_ne x 0 with rfl | hx
    ¬∑ simp
    rcases eq_or_ne y 0 with rfl | hy
    ¬∑ simp
    simp [hx, hy]
  nonneg' x := by rcases eq_or_ne x 0 with hx | hx <;> simp [hx]
  eq_zero' x := by rcases eq_or_ne x 0 with hx | hx <;> simp [hx]
  add_le' x y := by
    rcases eq_or_ne x 0 with rfl | hx
    ¬∑ simp
    rcases eq_or_ne y 0 with rfl | hy
    ¬∑ simp
    simp only [hx, ‚ÜìreduceIte, hy, show (1 : S) + 1 = 2 by norm_num]
    rcases eq_or_ne (x + y) 0 with hxy | hxy <;> simp [hxy, one_le_two]

@[simp]
lemma trivial_apply [DecidablePred fun x : R ‚Ü¶ x = 0] [NoZeroDivisors R] {S : Type*}
    [OrderedSemiring S] [Nontrivial S] {x : R} (hx : x ‚â† 0) :
    AbsoluteValue.trivial (S := S) x = 1 :=
  if_neg hx

/-- An absolute value that is equivalent to the trivial one is already trivial. -/
lemma eq_trivial_of_equiv_trivial [DecidablePred fun x : R ‚Ü¶ x = 0] [NoZeroDivisors R] {S : Type*}
    [OrderedSemiring S] [Nontrivial S] (f : AbsoluteValue R ‚Ñù) :
    f.equiv .trivial ‚Üî f = .trivial := by
  refine ‚ü®fun ‚ü®c, hc‚ÇÄ, hc‚ü© ‚Ü¶ ext fun x ‚Ü¶ ?_, fun H ‚Ü¶ H ‚ñ∏ equiv_refl f‚ü©
  apply_fun (¬∑ x) at hc
  rcases eq_or_ne x 0 with rfl | hx
  ¬∑ simp
  ¬∑ simp only [ne_eq, hx, not_false_eq_true, trivial_apply] at hc ‚ä¢
    exact (Real.rpow_left_inj (f.nonneg x) zero_le_one hc‚ÇÄ.ne').mp <| (Real.one_rpow c).symm ‚ñ∏ hc

/-- An absolute value satisfies `f n ‚â§ n` for every `n : ‚Ñï`. -/
lemma apply_nat_le_self {S : Type*} [OrderedRing S] [IsDomain S] (n : ‚Ñï) (f : AbsoluteValue R S) :
    f n ‚â§ n := by
  cases subsingleton_or_nontrivial R
  ¬∑ simp [Subsingleton.eq_zero (n : R)]
  induction n with
  | zero => simp
  | succ n hn =>
    simp only [Nat.cast_succ]
    calc
      f (n + 1) ‚â§ f n + f 1 := f.add_le' ..
      _ = f n + 1 := congrArg (f n + ¬∑) f.map_one
      _ ‚â§ n + 1 := add_le_add_right hn 1

open Int in
/-- An absolute value composed with the absolute value on integers equals
the absolute value itself. -/
lemma apply_natAbs_eq {R S : Type*} [Ring R] [OrderedCommRing S] [NoZeroDivisors S] (x : ‚Ñ§)
    (f : AbsoluteValue R S) :
    f (natAbs x) = f x := by
  obtain ‚ü®_, rfl | rfl‚ü© := eq_nat_or_neg x <;> simp

end AbsoluteValue

end API

/-! ### Preliminary lemmas on limits and lists -/


open Filter Nat Real Topology

-- For any `C > 0`, the limit of `C ^ (1/k)` is 1 as `k ‚Üí ‚àû`
private lemma tendsto_const_rpow_inv {C : ‚Ñù} (hC : 0 < C) :
    Tendsto (fun k : ‚Ñï ‚Ü¶ C ^ (k : ‚Ñù)‚Åª¬π) atTop (ùìù 1) :=
  ((continuous_iff_continuousAt.mpr fun _ ‚Ü¶ continuousAt_const_rpow hC.ne').tendsto'
    0 1 (rpow_zero C)).comp <| tendsto_inv_atTop_zero.comp tendsto_natCast_atTop_atTop

--extends the lemma `tendsto_rpow_div` when the function has natural input
private lemma tendsto_nat_rpow_inv :
    Tendsto (fun k : ‚Ñï ‚Ü¶ (k : ‚Ñù) ^ (k : ‚Ñù)‚Åª¬π) atTop (ùìù 1) := by
  simp_rw [‚Üê one_div]
  exact Tendsto.comp tendsto_rpow_div tendsto_natCast_atTop_atTop

-- Multiplication by a constant moves in a List.sum
private lemma list_mul_sum {R : Type*} [CommSemiring R] {T : Type*} (l : List T) (y : R) (x : R) :
    (l.mapIdx fun i _ => x * y ^ i).sum = x * (l.mapIdx fun i _ => y ^ i).sum := by
  simp_rw [‚Üê smul_eq_mul, List.smul_sum, List.mapIdx_eq_enum_map]
  congr 1
  simp

-- Geometric sum for lists
private lemma list_geom {T : Type*} {F : Type*} [Field F] (l : List T) {y : F} (hy : y ‚â† 1) :
    (l.mapIdx fun i _ => y ^ i).sum = (y ^ l.length - 1) / (y - 1) := by
  rw [‚Üê geom_sum_eq hy l.length, List.mapIdx_eq_enum_map, Finset.sum_range, ‚Üê Fin.sum_univ_get']
  simp only [List.getElem_enum, Function.uncurry_apply_pair]
  let e : Fin l.enum.length ‚âÉ Fin l.length := finCongr List.enum_length
  exact Fintype.sum_bijective e e.bijective _ _ fun _ ‚Ü¶ rfl

namespace Rat.AbsoluteValue

open Int AbsoluteValue

variable {f g : AbsoluteValue ‚Ñö ‚Ñù}

/-- Values of an absolute value on the rationals coincide on `‚Ñï` if and only if they coincide
on `‚Ñ§`. -/
lemma eq_on_nat_iff_eq_on_Int : (‚àÄ n : ‚Ñï , f n = g n) ‚Üî ‚àÄ n : ‚Ñ§ , f n = g n := by
  refine ‚ü®fun h z ‚Ü¶ ?_, fun a n ‚Ü¶ a n‚ü©
  obtain ‚ü®n , rfl | rfl‚ü© := eq_nat_or_neg z <;> simp [h n]

/-- Values of an absolute value on the rationals are determined by the values on the natural
numbers. -/
lemma eq_on_nat_iff_eq : (‚àÄ n : ‚Ñï , f n = g n) ‚Üî f = g := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h n ‚Ü¶ congrFun (congrArg DFunLike.coe h) ‚Üën‚ü©
  ext1 z
  rw [‚Üê Rat.num_div_den z, map_div‚ÇÄ, map_div‚ÇÄ, h, eq_on_nat_iff_eq_on_Int.mp h]

/-- The equivalence class of an absolute value on the rationals is determined by its values on
the natural numbers. -/
lemma equiv_on_nat_iff_equiv : (‚àÉ c : ‚Ñù, 0 < c ‚àß ‚àÄ n : ‚Ñï , f n ^ c = g n) ‚Üî f.equiv g := by
  refine ‚ü®fun ‚ü®c, hc, h‚ü© ‚Ü¶ ‚ü®c, hc, ?_‚ü©, fun ‚ü®c, hc, h‚ü© ‚Ü¶ ‚ü®c, hc, (congrFun h ¬∑)‚ü©‚ü©
  ext1 x
  rw [‚Üê Rat.num_div_den x, map_div‚ÇÄ, map_div‚ÇÄ, div_rpow (by positivity) (by positivity), h x.den,
    ‚Üê AbsoluteValue.apply_natAbs_eq,‚Üê AbsoluteValue.apply_natAbs_eq, h (natAbs x.num)]

section Non_archimedean

/-!
### The non-archimedean case

Every bounded absolute value is equivalent to a `p`-adic absolute value
-/

/-- The real-valued `AbsoluteValue` corresponding to the p-adic norm on `‚Ñö`. -/
def padic (p : ‚Ñï) [Fact p.Prime] : AbsoluteValue ‚Ñö ‚Ñù where
  toFun x := (padicNorm p x : ‚Ñù)
  map_mul' := by simp only [padicNorm.mul, Rat.cast_mul, forall_const]
  nonneg' x := cast_nonneg.mpr <| padicNorm.nonneg x
  eq_zero' x :=
    ‚ü®fun H ‚Ü¶ padicNorm.zero_of_padicNorm_eq_zero <| cast_eq_zero.mp H,
      fun H ‚Ü¶ cast_eq_zero.mpr <| H ‚ñ∏ padicNorm.zero (p := p)‚ü©
  add_le' x y := by simp only; exact_mod_cast padicNorm.triangle_ineq x y

@[simp] lemma padic_eq_padicNorm (p : ‚Ñï) [Fact p.Prime] (r : ‚Ñö) :
    padic p r = padicNorm p r := rfl

-- ## Step 1: define `p = minimal n s. t. 0 < f n < 1`

variable (hf_nontriv : f ‚â† AbsoluteValue.trivial) (bdd : ‚àÄ n : ‚Ñï, f n ‚â§ 1)

include hf_nontriv bdd in
/-- There exists a minimal positive integer with absolute value smaller than 1. -/
lemma exists_minimal_nat_zero_lt_and_lt_one :
    ‚àÉ p : ‚Ñï, (0 < f p ‚àß f p < 1) ‚àß ‚àÄ m : ‚Ñï, 0 < f m ‚àß f m < 1 ‚Üí p ‚â§ m := by
  -- There is a positive integer with absolute value different from one.
  obtain ‚ü®n, hn1, hn2‚ü© : ‚àÉ n : ‚Ñï, n ‚â† 0 ‚àß f n ‚â† 1 := by
    contrapose! hf_nontriv
    rw [AbsoluteValue.trivial, ‚Üê eq_on_nat_iff_eq]
    intro n
    rcases eq_or_ne n 0 with rfl | hn0
    ¬∑ simp
    ¬∑ simp [hn0, hf_nontriv n hn0]
  set P := {m : ‚Ñï | 0 < f ‚Üëm ‚àß f ‚Üëm < 1} -- p is going to be the minimum of this set.
  have hP : P.Nonempty :=
    ‚ü®n, map_pos_of_ne_zero f (Nat.cast_ne_zero.mpr hn1), lt_of_le_of_ne (bdd n) hn2‚ü©
  exact ‚ü®sInf P, Nat.sInf_mem hP, fun m hm ‚Ü¶ Nat.sInf_le hm‚ü©

-- ## Step 2: p is prime

variable {p : ‚Ñï} (hp0 : 0 < f p) (hp1 : f p < 1) (hmin : ‚àÄ m : ‚Ñï, 0 < f m ‚àß f m < 1 ‚Üí p ‚â§ m)

include hp0 hp1 hmin in
/-- The minimal positive integer with absolute value smaller than 1 is a prime number.-/
lemma is_prime_of_minimal_nat_zero_lt_and_lt_one : p.Prime := by
  rw [‚Üê Nat.irreducible_iff_nat_prime]
  constructor -- Two goals: p is not a unit and any product giving p must contain a unit.
  ¬∑ rw [Nat.isUnit_iff]
    rintro rfl
    simp only [Nat.cast_one, map_one, lt_self_iff_false] at hp1
  ¬∑ rintro a b rfl
    rw [Nat.isUnit_iff, Nat.isUnit_iff]
    by_contra! con
    obtain ‚ü®ha‚ÇÅ, hb‚ÇÅ‚ü© := con
    obtain ‚ü®ha‚ÇÄ, hb‚ÇÄ‚ü© : a ‚â† 0 ‚àß b ‚â† 0 := by
      refine mul_ne_zero_iff.mp fun h ‚Ü¶ ?_
      rwa [h, Nat.cast_zero, map_zero, lt_self_iff_false] at hp0
    have hap : a < a * b := lt_mul_of_one_lt_right (by omega) (by omega)
    have hbp : b < a * b := lt_mul_of_one_lt_left (by omega) (by omega)
    have ha :=
      le_of_not_lt <| not_and.mp ((hmin a).mt hap.not_le) (map_pos_of_ne_zero f (mod_cast ha‚ÇÄ))
    have hb :=
      le_of_not_lt <| not_and.mp ((hmin b).mt hbp.not_le) (map_pos_of_ne_zero f (mod_cast hb‚ÇÄ))
    rw [Nat.cast_mul, map_mul] at hp1
    exact ((one_le_mul_of_one_le_of_one_le ha hb).trans_lt hp1).false

-- ## Step 3: if p does not divide m, then f m = 1

open Real

include hp0 hp1 hmin bdd in
/-- A natural number not divible by `p` has absolute value 1. -/
lemma eq_one_of_not_dvd {m : ‚Ñï} (hpm : ¬¨ p ‚à£ m) : f m = 1 := by
  apply le_antisymm (bdd m)
  by_contra! hm
  set M := f p ‚äî f m with hM
  set k := Nat.ceil (M.logb (1 / 2)) + 1 with hk
  obtain ‚ü®a, b, bezout‚ü© : IsCoprime (p ^ k : ‚Ñ§) (m ^ k) :=
    is_prime_of_minimal_nat_zero_lt_and_lt_one hp0 hp1 hmin
      |>.coprime_iff_not_dvd |>.mpr hpm |>.isCoprime |>.pow
  have le_half {x} (hx0 : 0 < x) (hx1 : x < 1) (hxM : x ‚â§ M) : x ^ k < 1 / 2 := by
    calc
    x ^ k = x ^ (k : ‚Ñù) := (rpow_natCast x k).symm
    _ < x ^ M.logb (1 / 2) := by
      apply rpow_lt_rpow_of_exponent_gt hx0 hx1
      rw [hk]
      push_cast
      exact lt_add_of_le_of_pos (Nat.le_ceil _) zero_lt_one
    _ ‚â§ x ^ x.logb (1 / 2) := by
      apply rpow_le_rpow_of_exponent_ge hx0 hx1.le
      simp only [one_div, ‚Üê log_div_log, log_inv, neg_div, ‚Üê div_neg, hM]
      gcongr
      simp only [Left.neg_pos_iff]
      exact log_neg (lt_sup_iff.mpr <| .inl hp0) (sup_lt_iff.mpr ‚ü®hp1, hm‚ü©)
    _ = 1 / 2 := rpow_logb hx0 hx1.ne one_half_pos
  apply lt_irrefl (1 : ‚Ñù)
  calc
  1 = f 1 := (map_one f).symm
  _ = f (a * p ^ k + b * m ^ k) := by rw_mod_cast [bezout]; norm_cast
  _ ‚â§ f (a * p ^ k) + f (b * m ^ k) := f.add_le' ..
  _ ‚â§ 1 * (f p) ^ k + 1 * (f m) ^ k := by
    simp only [map_mul, map_pow]
    gcongr
    all_goals rw [‚Üê AbsoluteValue.apply_natAbs_eq]; apply bdd
  _ = (f p) ^ k + (f m) ^ k := by simp only [one_mul]
  _ < 1 := by
    have hm‚ÇÄ : 0 < f m := f.pos <| Nat.cast_ne_zero.mpr fun H ‚Ü¶ hpm <| H ‚ñ∏ dvd_zero p
    linarith only [le_half hp0 hp1 le_sup_left, le_half hm‚ÇÄ hm le_sup_right]

-- ## Step 4: f p = p ^ (-t) for some positive real t

include hp0 hp1 hmin in
/-- The absolute value of `p` is `p ^ (-t)` for some positive real number `t`. -/
lemma exists_pos_eq_pow_neg : ‚àÉ t : ‚Ñù, 0 < t ‚àß f p = p ^ (-t) := by
  have pprime := is_prime_of_minimal_nat_zero_lt_and_lt_one hp0 hp1 hmin
  refine ‚ü®- logb p (f p), Left.neg_pos_iff.mpr <| logb_neg (mod_cast pprime.one_lt) hp0 hp1, ?_‚ü©
  rw [neg_neg]
  exact (rpow_logb (mod_cast pprime.pos) (mod_cast pprime.ne_one) hp0).symm

-- ## Non-archimedean case: end goal

include hf_nontriv bdd in
/-- If `f` is bounded and not trivial, then it is equivalent to a p-adic absolute value. -/
theorem equiv_padic_of_bounded :
    ‚àÉ! p, ‚àÉ (_ : Fact p.Prime), f.equiv (padic p) := by
  obtain ‚ü®p, hfp, hmin‚ü© := exists_minimal_nat_zero_lt_and_lt_one hf_nontriv bdd
  have hprime := is_prime_of_minimal_nat_zero_lt_and_lt_one hfp.1 hfp.2 hmin
  have hprime_fact : Fact p.Prime := ‚ü®hprime‚ü©
  obtain ‚ü®t, h‚ü© := exists_pos_eq_pow_neg hfp.1 hfp.2 hmin
  simp_rw [‚Üê equiv_on_nat_iff_equiv]
  refine ‚ü®p, ‚ü®hprime_fact, t‚Åª¬π, inv_pos_of_pos h.1, fun n ‚Ü¶ ?_‚ü©, fun q ‚ü®hq_prime, h_equiv‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ have ht : t‚Åª¬π ‚â† 0 := inv_ne_zero h.1.ne'
    rcases eq_or_ne n 0 with rfl | hn -- Separate cases n = 0 and n ‚â† 0
    ¬∑ simp [ht]
    ¬∑ /- Any natural number can be written as a power of p times a natural number not divisible
      by p  -/
      rcases Nat.exists_eq_pow_mul_and_not_dvd hn p hprime.ne_one with ‚ü®e, m, hpm, rfl‚ü©
      simp only [Nat.cast_mul, Nat.cast_pow, map_mul, map_pow, h.2,
        eq_one_of_not_dvd bdd hfp.1 hfp.2 hmin hpm, padic_eq_padicNorm,
        padicNorm.padicNorm_p_of_prime, cast_inv, cast_natCast, inv_pow]
      rw [‚Üê padicNorm.nat_eq_one_iff] at hpm
      simp only [‚Üê rpow_natCast, p.cast_nonneg, ‚Üê rpow_mul, neg_mul, mul_one, ‚Üê rpow_neg, hpm,
        cast_one]
      congr
      field_simp [h.1.ne']
  ¬∑ by_contra! hne
    apply hq_prime.elim.prime.ne_one
    rw [ne_comm, ‚Üê Nat.coprime_primes hprime hq_prime.elim, hprime.coprime_iff_not_dvd] at hne
    rcases h_equiv with ‚ü®c, _, h_eq‚ü©
    have h_eq' := h_eq q
    simp only [eq_one_of_not_dvd bdd hfp.1 hfp.2 hmin hne, one_rpow, padic_eq_padicNorm,
      padicNorm.padicNorm_p_of_prime, cast_inv, cast_natCast, eq_comm, inv_eq_one] at h_eq'
    exact_mod_cast h_eq'

end Non_archimedean

section Archimedean

/-! ## Archimedean case

Every unbounded absolute value is equivalent to the standard absolute value
-/

/-- The usual absolute value on `‚Ñö`. -/
def real : AbsoluteValue ‚Ñö ‚Ñù where
  toFun x := |x|
  map_mul' x y := by simpa using abs_mul (x : ‚Ñù) (y : ‚Ñù)
  nonneg' x := by simp
  eq_zero' x := by simp
  add_le' x y := by simpa using abs_add_le (x : ‚Ñù) (y : ‚Ñù)

@[simp] lemma real_eq_abs (r : ‚Ñö) : real r = |r| := by
  simp [real]

-- ## Preliminary result

/-- Given an two integers `n`, `m` with `m > 1`, the absolute value of `n` is bounded by
`m + m * f m + m * (f m) ^ 2 + ... + m * (f m) ^ d` where `d` is the number of digits of the
expansion of `n` in base `m`. -/
lemma apply_le_sum_digits (n : ‚Ñï) {m : ‚Ñï} (hm : 1 < m) :
    f n ‚â§ ((Nat.digits m n).mapIdx fun i _ ‚Ü¶ m * (f m) ^ i).sum := by
  set L := Nat.digits m n
  set L' : List ‚Ñö := List.map Nat.cast (L.mapIdx fun i a ‚Ü¶ (a * m ^ i)) with hL'
  -- If `c` is a digit in the expansion of `n` in base `m`, then `f c` is less than `m`.
  have hcoef {c : ‚Ñï} (hc : c ‚àà Nat.digits m n) : f c < m :=
    lt_of_le_of_lt (f.apply_nat_le_self c) (mod_cast Nat.digits_lt_base hm hc)
  calc
  f n = f ((Nat.ofDigits m L : ‚Ñï) : ‚Ñö) := by rw [Nat.ofDigits_digits m n]
    _ = f (L'.sum) := by rw [Nat.ofDigits_eq_sum_mapIdx]; norm_cast
    _ ‚â§ (L'.map f).sum := AbsoluteValue.listSum_le L' f
    _ ‚â§ (L.mapIdx fun i _ ‚Ü¶ m * (f m) ^ i).sum := ?_
  simp only [hL', List.mapIdx_eq_enum_map, List.map_map]
  apply List.sum_le_sum
  rintro ‚ü®i, a‚ü© hia
  dsimp [Function.uncurry]
  replace hia := List.mem_enumFrom hia
  push_cast
  rw [map_mul, map_pow]
  gcongr
  simp only [zero_le, zero_add, tsub_zero, true_and] at hia
  exact (hcoef (List.mem_iff_get.mpr ‚ü®‚ü®i, hia.1‚ü©, hia.2.symm‚ü©)).le

-- ## Step 1: if f is an AbsoluteValue and f n > 1 for some natural n, then f n > 1 for all n ‚â• 2

/-- If `f n > 1` for some `n` then `f n > 1` for all `n ‚â• 2` -/
lemma one_lt_of_not_bounded (notbdd : ¬¨ ‚àÄ n : ‚Ñï, f n ‚â§ 1) {n‚ÇÄ : ‚Ñï} (hn‚ÇÄ : 1 < n‚ÇÄ) : 1 < f n‚ÇÄ := by
  contrapose! notbdd with h
  intro n
  have h_ineq1 {m : ‚Ñï} (hm : 1 ‚â§ m) : f m ‚â§ n‚ÇÄ * (logb n‚ÇÄ m + 1) := by
    /- L is the string of digits of `n` in the base `n‚ÇÄ`-/
    set L := Nat.digits n‚ÇÄ m
    calc
    f m ‚â§ (L.mapIdx fun i _ ‚Ü¶ n‚ÇÄ * f n‚ÇÄ ^ i).sum := apply_le_sum_digits m hn‚ÇÄ
    _ ‚â§ (L.mapIdx fun _ _ ‚Ü¶ (n‚ÇÄ : ‚Ñù)).sum := by
      simp only [List.mapIdx_eq_enum_map, List.map_map]
      apply List.sum_le_sum
      rintro ‚ü®i, a‚ü© _
      simp only [Function.comp_apply, Function.uncurry_apply_pair]
      exact mul_le_of_le_of_le_one' (mod_cast le_refl n‚ÇÄ) (pow_le_one‚ÇÄ (by positivity) h)
        (by positivity) (by positivity)
    _ = n‚ÇÄ * (Nat.log n‚ÇÄ m + 1) := by
      rw [List.mapIdx_eq_enum_map, List.eq_replicate_of_mem (a := (n‚ÇÄ : ‚Ñù))
        (l := List.map (Function.uncurry fun _ _ ‚Ü¶ n‚ÇÄ) (List.enum L)),
        List.sum_replicate, List.length_map, List.enum_length, nsmul_eq_mul, mul_comm,
        Nat.digits_len n‚ÇÄ m hn‚ÇÄ (not_eq_zero_of_lt hm), Nat.cast_add_one]
      simp +contextual
    _ ‚â§ n‚ÇÄ * (logb n‚ÇÄ m + 1) := by gcongr; exact natLog_le_logb ..
  -- For h_ineq2 we need to exclude the case n = 0.
  rcases eq_or_ne n 0 with rfl | h‚ÇÄ
  ¬∑ simp
  have h_ineq2 (k : ‚Ñï) (hk : 0 < k) :
      f n ‚â§ (n‚ÇÄ * (logb n‚ÇÄ n + 1)) ^ (k : ‚Ñù)‚Åª¬π * k ^ (k : ‚Ñù)‚Åª¬π := by
    have : 0 ‚â§ logb n‚ÇÄ n := logb_nonneg (one_lt_cast.mpr hn‚ÇÄ) (mod_cast Nat.one_le_of_lt h‚ÇÄ.bot_lt)
    calc
    f n = (f ‚Üë(n ^ k)) ^ (k : ‚Ñù)‚Åª¬π := by
      rw [Nat.cast_pow, map_pow, ‚Üê rpow_natCast, rpow_rpow_inv (by positivity) (by positivity)]
    _  ‚â§ (n‚ÇÄ * (logb n‚ÇÄ ‚Üë(n ^ k) + 1)) ^ (k : ‚Ñù)‚Åª¬π := by
      gcongr
      exact h_ineq1 <| one_le_pow‚ÇÄ (one_le_iff_ne_zero.mpr h‚ÇÄ)
    _  = (n‚ÇÄ * (k * logb n‚ÇÄ n + 1)) ^ (k : ‚Ñù)‚Åª¬π := by
      rw [Nat.cast_pow, logb_pow]
    _  ‚â§ (n‚ÇÄ * (k * logb n‚ÇÄ n + k)) ^ (k : ‚Ñù)‚Åª¬π := by
      gcongr
      exact one_le_cast.mpr hk
    _ = (n‚ÇÄ * (logb n‚ÇÄ n + 1)) ^ (k : ‚Ñù)‚Åª¬π * k ^ (k : ‚Ñù)‚Åª¬π := by
      rw [‚Üê mul_rpow (by positivity) (by positivity), mul_assoc, add_mul, one_mul,
        mul_comm _ (k : ‚Ñù)]
-- For 0 < logb n‚ÇÄ n below we also need to exclude n = 1.
  rcases eq_or_ne n 1 with rfl | h‚ÇÅ
  ¬∑ simp
  refine le_of_tendsto_of_tendsto tendsto_const_nhds ?_ (eventually_atTop.mpr ‚ü®1, h_ineq2‚ü©)
  nth_rw 2 [‚Üê mul_one 1]
  have : 0 < logb n‚ÇÄ n := logb_pos (mod_cast hn‚ÇÄ) (by norm_cast; omega)
  exact Tendsto.mul (tendsto_const_rpow_inv (by positivity)) tendsto_nat_rpow_inv

-- ## Step 2: given m,n ‚â• 2 and |m|=m^s, |n|=n^t for s,t > 0, we have t ‚â§ s

variable {m n : ‚Ñï} (hm : 1 < m) (hn : 1 < n) (notbdd : ¬¨ ‚àÄ (n : ‚Ñï), f n ‚â§ 1)

include hm notbdd in
private lemma expr_pos : 0 < m * f m / (f m - 1) := by
  apply div_pos (mul_pos (mod_cast zero_lt_of_lt hm)
    (map_pos_of_ne_zero f (mod_cast ne_zero_of_lt hm)))
  linarith only [one_lt_of_not_bounded notbdd hm]

include hn hm notbdd in
private lemma param_upperbound {k : ‚Ñï} (hk : k ‚â† 0) :
    f n ‚â§ (m * f m / (f m - 1)) ^ (k : ‚Ñù)‚Åª¬π * f m ^ logb m n := by
  have h_ineq1 {m n : ‚Ñï} (hm : 1 < m) (hn : 1 < n) :
      f n ‚â§ (m * f m / (f m - 1)) * f m ^ logb m n := by
    let d := Nat.log m n
    calc
    f n ‚â§ ((Nat.digits m n).mapIdx fun i _ ‚Ü¶ m * f m ^ i).sum :=
      apply_le_sum_digits n hm
    _ = m * ((Nat.digits m n).mapIdx fun i _ ‚Ü¶ f m ^ i).sum := list_mul_sum (m.digits n) (f m) m
    _ = m * ((f m ^ (d + 1) - 1) / (f m - 1)) := by
      rw [list_geom _ (ne_of_gt (one_lt_of_not_bounded notbdd hm)),
        (Nat.digits_len m n hm (not_eq_zero_of_lt hn)).symm]
    _ ‚â§ m * ((f m ^ (d + 1)) / (f m - 1)) := by
      gcongr
      ¬∑ linarith only [one_lt_of_not_bounded notbdd hm]
      ¬∑ simp only [tsub_le_iff_right, le_add_iff_nonneg_right, zero_le_one]
    _ = ‚Üëm * f ‚Üëm / (f ‚Üëm - 1) * f ‚Üëm ^ d := by ring
    _ ‚â§ ‚Üëm * f ‚Üëm / (f ‚Üëm - 1) * f ‚Üëm ^ logb ‚Üëm ‚Üën := by
      gcongr
      ¬∑ exact (expr_pos hm notbdd).le
      ¬∑ rw [‚Üê rpow_natCast, rpow_le_rpow_left_iff (one_lt_of_not_bounded notbdd hm)]
        exact natLog_le_logb n m
  apply le_of_pow_le_pow_left‚ÇÄ hk <| mul_nonneg (rpow_nonneg (expr_pos hm notbdd).le _)
    (rpow_nonneg (apply_nonneg f ‚Üëm) _)
  nth_rewrite 2 [‚Üê rpow_natCast]
  rw [mul_rpow (rpow_nonneg (expr_pos hm notbdd).le _) (rpow_nonneg (apply_nonneg f ‚Üëm) _),
    ‚Üê rpow_mul (expr_pos hm notbdd).le, ‚Üê rpow_mul (apply_nonneg f ‚Üëm),
    inv_mul_cancel‚ÇÄ (mod_cast hk), rpow_one, mul_comm (logb ..)]
  calc
    (f n) ^ k = f ‚Üë(n ^ k) := by simp
    _ ‚â§ (m * f m / (f m - 1)) * f m ^ logb m ‚Üë(n ^ k) := h_ineq1 hm (Nat.one_lt_pow hk hn)
    _ = (m * f m / (f m - 1)) * f m ^ (k * logb m n) := by
      rw [Nat.cast_pow, logb_pow]

include hm hn notbdd in
/-- Given two natural numbers `n, m` greater than 1 we have `f n ‚â§ f m ^ logb m n`. -/
lemma le_pow_log : f n ‚â§ f m ^ logb m n := by
  have : Tendsto (fun k : ‚Ñï ‚Ü¶ (m * f m / (f m - 1)) ^ (k : ‚Ñù)‚Åª¬π * f m ^ logb m n)
      atTop (ùìù (f m ^ logb m n)) := by
    nth_rw 2 [‚Üê one_mul (f ‚Üëm ^ logb ‚Üëm ‚Üën)]
    exact (tendsto_const_rpow_inv (expr_pos hm notbdd)).mul_const _
  exact le_of_tendsto_of_tendsto (tendsto_const_nhds (x:= f ‚Üën)) this <|
    eventually_atTop.mpr ‚ü®2, fun b hb ‚Ü¶ param_upperbound hm hn notbdd (not_eq_zero_of_lt hb)‚ü©

include hm hn notbdd in
/-- Given `m, n ‚â• 2` and `f m = m ^ s`, `f n = n ^ t` for `s, t > 0`, we have `t ‚â§ s`. -/
lemma le_of_eq_pow {s t : ‚Ñù} (hfm : f m = m ^ s) (hfn : f n = n ^ t)  : t ‚â§ s := by
  rw [‚Üê rpow_le_rpow_left_iff (x := n) (mod_cast hn), ‚Üê hfn]
  apply le_trans <| le_pow_log hm hn notbdd
  rw [hfm, ‚Üê rpow_mul (Nat.cast_nonneg m), mul_comm, rpow_mul (Nat.cast_nonneg m),
    rpow_logb (mod_cast zero_lt_of_lt hm) (mod_cast hm.ne') (mod_cast zero_lt_of_lt hn)]

include hm hn notbdd in
private lemma symmetric_roles {s t : ‚Ñù} (hfm : f m = m ^ s) (hfn : f n = n ^ t) : s = t :=
  le_antisymm (le_of_eq_pow hn hm notbdd hfn hfm) (le_of_eq_pow hm hn notbdd hfm hfn)

-- ## Archimedean case: end goal

include notbdd in
/-- If `f` is not bounded and not trivial, then it is equivalent to the standard absolute value on
`‚Ñö`. -/
theorem equiv_real_of_unbounded : f.equiv real := by
  obtain ‚ü®m, hm‚ü© := Classical.exists_not_of_not_forall notbdd
  have oneltm : 1 < m := by
    contrapose! hm
    rcases le_one_iff_eq_zero_or_eq_one.mp hm with rfl | rfl <;> simp
  rw [‚Üê equiv_on_nat_iff_equiv]
  set s := logb m (f m) with hs
  refine ‚ü®s‚Åª¬π,
    inv_pos.mpr (logb_pos (Nat.one_lt_cast.mpr oneltm) (one_lt_of_not_bounded notbdd oneltm)),
    fun n ‚Ü¶ ?_‚ü©
  rcases lt_trichotomy n 1 with h | rfl | h
  ¬∑ obtain rfl : n = 0 := by omega
    have : (logb (‚Üëm) (f ‚Üëm))‚Åª¬π ‚â† 0 := by
      simp only [ne_eq, inv_eq_zero, logb_eq_zero, Nat.cast_eq_zero, Nat.cast_eq_one, map_eq_zero,
        not_or]
      exact ‚ü®not_eq_zero_of_lt oneltm, oneltm.ne', by norm_cast,
        not_eq_zero_of_lt oneltm, ne_of_not_le hm, by linarith only [apply_nonneg f ‚Üëm]‚ü©
    simp [hs, this]
  ¬∑ simp
  ¬∑ simp only [real_eq_abs, abs_cast, Rat.cast_natCast]
    rw [rpow_inv_eq (apply_nonneg f ‚Üën) (Nat.cast_nonneg n)
      (logb_ne_zero_of_pos_of_ne_one (one_lt_cast.mpr oneltm) (by linarith only [hm])
      (by linarith only [hm]))]
    have hfm : f m = m ^ s := by
      rw [rpow_logb (mod_cast zero_lt_of_lt oneltm) (mod_cast oneltm.ne') (by linarith only [hm])]
    have hfn : f n = n ^ logb n (f n) := by
      rw [rpow_logb (mod_cast zero_lt_of_lt h) (mod_cast h.ne')
      (by apply map_pos_of_ne_zero; exact_mod_cast not_eq_zero_of_lt h)]
    rwa [‚Üê hs, symmetric_roles oneltm h notbdd hfm hfn]

end Archimedean

/-- **Ostrowski's Theorem**: every absolute value (with values in `‚Ñù`) on `‚Ñö` is equivalent
to either the standard absolute value or a `p`-adic absolute value for a prime `p`. -/
theorem equiv_real_or_padic (f : AbsoluteValue ‚Ñö ‚Ñù) (hf_nontriv : f ‚â† .trivial) :
    f.equiv real ‚à® ‚àÉ! p, ‚àÉ (_ : Fact p.Prime), f.equiv (padic p) := by
  by_cases bdd : ‚àÄ n : ‚Ñï, f n ‚â§ 1
  ¬∑ exact .inr <| equiv_padic_of_bounded hf_nontriv bdd
  ¬∑ exact .inl <| equiv_real_of_unbounded bdd

/-- The standard absolute value on `‚Ñö` is not equivalent to any `p`-adic absolute value. -/
lemma not_real_equiv_padic (p : ‚Ñï) [Fact p.Prime] : ¬¨ real.equiv (padic p) := by
  have hp : p.Prime := Fact.out
  rintro ‚ü®c, hc‚ÇÄ, hc‚ü©
  apply_fun (¬∑ 2) at hc
  simp only [real_eq_abs, abs_ofNat, cast_ofNat, padic_eq_padicNorm, ne_eq, OfNat.ofNat_ne_zero,
    not_false_eq_true, padicNorm.eq_zpow_of_nonzero, zpow_neg, cast_inv, cast_zpow,
    cast_natCast] at hc
  -- hc : 2 ^ c = (‚Üëp ^ padicValRat p 2)‚Åª¬π
  exact (((inv_le_one‚ÇÄ <| zpow_pos (mod_cast hp.pos) _).mpr <| mod_cast NeZero.one_le).trans_lt <|
    one_lt_rpow one_lt_two hc‚ÇÄ).ne' hc

end Rat.AbsoluteValue
