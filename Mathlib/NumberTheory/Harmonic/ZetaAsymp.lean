/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.NumberTheory.LSeries.RiemannZeta
import Mathlib.NumberTheory.Harmonic.GammaDeriv


/-!
# Asymptotics of `Î¶ s` as `s â†’ 1`

The goal of this file is to evaluate the limit of `Î¶ s - 1 / (s - 1)` as `s â†’ 1`.

### Main results

* `tendsto_riemannZeta_sub_one_div`: the limit of `Î¶ s - 1 / (s - 1)`, at the filter of punctured
  neighbourhoods of 1 in `â„‚`, exists and is equal to the Euler-Mascheroni constant `Î³`.
* `riemannZeta_one_ne_zero`: with our definition of `Î¶ 1` (which is characterised as the limit of
  `Î¶ s - 1 / (s - 1) / Gammaâ„ s` as `s â†’ 1`), we have `Î¶ 1 â‰  0`.

### Outline of arguments

We consider the sum `F s = âˆ‘' n : â„•, f (n + 1) s`, where `s` is a real variable and
`f n s = âˆ« x in n..(n + 1), (x - n) / x ^ (s + 1)`. We show that `F s` is continuous on `[1, âˆ)`,
that `F 1 = 1 - Î³`, and that `F s = 1 / (s - 1) - Î¶ s / s` for `1 < s`.

By combining these formulae, one deduces that the limit of `Î¶ s - 1 / (s - 1)` at `ğ“[>] (1 : â„)`
exists and is equal to `Î³`. Finally, using this and the Riemann removable singularity criterion
we obtain the limit along punctured neighbourhoods of 1 in `â„‚`.
-/

open Real Set MeasureTheory Filter Topology

@[inherit_doc] local notation "Î³" => eulerMascheroniConstant

namespace ZetaAsymptotics
-- since the intermediate lemmas are of little interest in themselves we put them in a namespace

/-!
##Â Definitions
-/

/-- Auxiliary function used in studying zeta-function asymptotics. -/
noncomputable def term (n : â„•) (s : â„) : â„ := âˆ« x : â„ in n..(n + 1), (x - n) / x ^ (s + 1)

/-- Sum of finitely many `term`s. -/
noncomputable def term_sum (s : â„) (N : â„•) : â„ := âˆ‘ n âˆˆ Finset.range N, term (n + 1) s

/-- Topological sum of `term`s. -/
noncomputable def term_tsum (s : â„) : â„ := âˆ‘' n, term (n + 1) s

lemma term_nonneg (n : â„•) (s : â„) : 0 â‰¤ term n s := by
  rw [term, intervalIntegral.integral_of_le (by simp)]
  refine setIntegral_nonneg measurableSet_Ioc (fun x hx â†¦ ?_)
  refine div_nonneg ?_ (rpow_nonneg ?_ _)
  all_goals linarith [hx.1]

lemma term_welldef {n : â„•} (hn : 0 < n) {s : â„} (hs : 0 < s) :
    IntervalIntegrable (fun x : â„ â†¦ (x - n) / x ^ (s + 1)) volume n (n + 1) := by
  rw [intervalIntegrable_iff_integrableOn_Icc_of_le (by linarith)]
  refine (ContinuousAt.continuousOn fun x hx â†¦ ContinuousAt.div ?_ ?_ ?_).integrableOn_Icc
  Â· fun_prop
  Â· apply continuousAt_id.rpow_const (Or.inr <| by linarith)
  Â· exact (rpow_pos_of_pos ((Nat.cast_pos.mpr hn).trans_le hx.1) _).ne'

section s_eq_one

/-!
## Evaluation of the sum for `s = 1`
-/

lemma term_one {n : â„•} (hn : 0 < n) :
    term n 1 = (log (n + 1) - log n) -  1 / (n + 1) := by
  have hv : âˆ€ x âˆˆ uIcc (n : â„) (n + 1), 0 < x := by
    intro x hx
    rw [uIcc_of_le (by simp only [le_add_iff_nonneg_right, zero_le_one])] at hx
    exact (Nat.cast_pos.mpr hn).trans_le hx.1
  calc term n 1
    _ = âˆ« x : â„ in n..(n + 1), (x - n) / x ^ 2 := by
      simp_rw [term, one_add_one_eq_two, â† Nat.cast_two (R := â„), rpow_natCast]
    _ = âˆ« x : â„ in n..(n + 1), (1 / x - n / x ^ 2) := by
      refine intervalIntegral.integral_congr (fun x hx â†¦ ?_)
      field_simp [(hv x hx).ne']
      ring
    _ = (âˆ« x : â„ in n..(n + 1), 1 / x) - n * âˆ« x : â„ in n..(n + 1), 1 / x ^ 2 := by
      simp_rw [â† mul_one_div (n : â„)]
      rw [intervalIntegral.integral_sub]
      Â· simp_rw [intervalIntegral.integral_const_mul]
      Â· exact intervalIntegral.intervalIntegrable_one_div (fun x hx â†¦ (hv x hx).ne') (by fun_prop)
      Â· exact (intervalIntegral.intervalIntegrable_one_div
          (fun x hx â†¦ (sq_pos_of_pos (hv x hx)).ne') (by fun_prop)).const_mul _
    _ = (log (â†‘n + 1) - log â†‘n) - n * âˆ« x : â„ in n..(n + 1), 1 / x ^ 2 := by
      congr 1
      rw [integral_one_div_of_pos, log_div]
      all_goals positivity
    _ = (log (â†‘n + 1) - log â†‘n) - n * âˆ« x : â„ in n..(n + 1), x ^ (-2 : â„) := by
      congr 2
      refine intervalIntegral.integral_congr (fun x hx â†¦ ?_)
      rw [rpow_neg, one_div, â† Nat.cast_two (R := â„), rpow_natCast]
      exact (hv x hx).le
    _ = log (â†‘n + 1) - log â†‘n - n * (1 / n - 1 / (n + 1)) := by
      rw [integral_rpow]
      Â· simp_rw [sub_div, (by norm_num : (-2 : â„) + 1 = -1), div_neg, div_one, neg_sub_neg,
          rpow_neg_one, â† one_div]
      Â· refine Or.inr âŸ¨by norm_num, not_mem_uIcc_of_lt ?_ ?_âŸ©
        all_goals positivity
    _ = log (â†‘n + 1) - log â†‘n - 1 / (â†‘n + 1) := by
      congr 1
      field_simp

lemma term_sum_one (N : â„•) : term_sum 1 N = log (N + 1) - harmonic (N + 1) + 1 := by
  induction' N with N hN
  Â· simp_rw [term_sum, Finset.sum_range_zero, harmonic_succ, harmonic_zero,
      Nat.cast_zero, zero_add, Nat.cast_one, inv_one, Rat.cast_one, log_one, sub_add_cancel]
  Â· unfold term_sum at hN âŠ¢
    rw [Finset.sum_range_succ, hN, harmonic_succ (N + 1),
      term_one (by positivity : 0 < N + 1)]
    push_cast
    ring_nf

/-- The topological sum of `ZetaAsymptotics.term (n + 1) 1` over all `n : â„•` is `1 - Î³`. This is
proved by directly evaluating the sum of the first `N` terms and using the limit definition of `Î³`.
-/
lemma term_tsum_one : HasSum (fun n â†¦ term (n + 1) 1) (1 - Î³) := by
  rw [hasSum_iff_tendsto_nat_of_nonneg (fun n â†¦ term_nonneg (n + 1) 1)]
  show Tendsto (fun N â†¦ term_sum 1 N) atTop _
  simp_rw [term_sum_one, sub_eq_neg_add]
  refine Tendsto.add ?_ tendsto_const_nhds
  have := (tendsto_eulerMascheroniSeq'.comp (tendsto_add_atTop_nat 1)).neg
  refine this.congr' (eventually_of_forall (fun n â†¦ ?_))
  simp_rw [Function.comp_apply, eulerMascheroniSeq', if_false]
  push_cast
  abel

end s_eq_one

section s_gt_one

/-!
## Evaluation of the sum for `1 < s`
-/

lemma term_of_lt {n : â„•} (hn : 0 < n) {s : â„} (hs : 1 < s) :
    term n s = 1 / (s - 1) * (1 / n ^ (s - 1) - 1 / (n + 1) ^ (s - 1))
    - n / s * (1 / n ^ s - 1 / (n + 1) ^ s) := by
  have hv : âˆ€ x âˆˆ uIcc (n : â„) (n + 1), 0 < x := by
    intro x hx
    rw [uIcc_of_le (by simp only [le_add_iff_nonneg_right, zero_le_one])] at hx
    exact (Nat.cast_pos.mpr hn).trans_le hx.1
  calc term n s
    _ = âˆ« x : â„ in n..(n + 1), (x - n) / x ^ (s + 1) := by rfl
    _ = âˆ« x : â„ in n..(n + 1), (x ^ (-s) - n * x ^ (-(s + 1))) := by
      refine intervalIntegral.integral_congr (fun x hx â†¦ ?_)
      rw [sub_div, rpow_add_one (hv x hx).ne', mul_comm, â† div_div, div_self (hv x hx).ne',
        rpow_neg (hv x hx).le, rpow_neg (hv x hx).le, one_div, rpow_add_one (hv x hx).ne', mul_comm,
        div_eq_mul_inv]
    _ = (âˆ« x : â„ in n..(n + 1), x ^ (-s)) - n * (âˆ« x : â„ in n..(n + 1), x ^ (-(s + 1))) := by
      rw [intervalIntegral.integral_sub, intervalIntegral.integral_const_mul] <;>
      [skip; apply IntervalIntegrable.const_mul] <;>
      Â· refine intervalIntegral.intervalIntegrable_rpow (Or.inr <| not_mem_uIcc_of_lt ?_ ?_)
        Â· exact_mod_cast hn
        Â· linarith
    _ = 1 / (s - 1) * (1 / n ^ (s - 1) - 1 / (n + 1) ^ (s - 1))
          - n / s * (1 / n ^ s - 1 / (n + 1) ^ s) := by
      have : 0 âˆ‰ uIcc (n : â„) (n + 1) := (lt_irrefl _ <| hv _ Â·)
      rw [integral_rpow (Or.inr âŸ¨by linarith, thisâŸ©), integral_rpow (Or.inr âŸ¨by linarith, thisâŸ©)]
      congr 1
      Â· rw [show -s + 1 = -(s - 1) by ring, div_neg, â† neg_div, mul_comm, mul_one_div, neg_sub,
          rpow_neg (Nat.cast_nonneg _), one_div, rpow_neg (by linarith), one_div]
      Â· rw [show -(s + 1) + 1 = -s by ring, div_neg, â† neg_div, neg_sub, div_mul_eq_mul_div,
          mul_div_assoc, rpow_neg (Nat.cast_nonneg _), one_div, rpow_neg (by linarith), one_div]

lemma term_sum_of_lt (N : â„•) {s : â„} (hs : 1 < s) :
    term_sum s N = 1 / (s - 1) * (1 - 1 / (N + 1) ^ (s - 1))
    - 1 / s * ((âˆ‘ n âˆˆ Finset.range N, 1 / (n + 1 : â„) ^ s) - N / (N + 1) ^ s) := by
  simp only [term_sum]
  conv => enter [1, 2, n]; rw [term_of_lt (by simp) hs]
  rw [Finset.sum_sub_distrib]
  congr 1
  Â· induction' N with N hN
    Â· simp
    Â· rw [Finset.sum_range_succ, hN, Nat.cast_add_one]
      ring_nf
  Â· simp_rw [mul_comm (_ / _), â† mul_div_assoc, div_eq_mul_inv _ s, â† Finset.sum_mul, mul_one]
    congr 1
    induction' N with N hN
    Â· simp
    Â· simp_rw [Finset.sum_range_succ, hN, Nat.cast_add_one, sub_eq_add_neg, add_assoc]
      congr 1
      ring_nf

/-- For `1 < s`, the topological sum of `ZetaAsymptotics.term (n + 1) s` over all `n : â„•` is
`1 / (s - 1) - Î¶ s / s`.
-/
lemma term_tsum_of_lt {s : â„} (hs : 1 < s) :
    term_tsum s = (1 / (s - 1) - 1 / s * âˆ‘' n : â„•, 1 / (n + 1 : â„) ^ s) := by
  apply HasSum.tsum_eq
  rw [hasSum_iff_tendsto_nat_of_nonneg (fun n â†¦ term_nonneg (n + 1) s)]
  change Tendsto (fun N â†¦ term_sum s N) atTop _
  simp_rw [term_sum_of_lt _ hs]
  apply Tendsto.sub
  Â· rw [show ğ“ (1 / (s - 1)) = ğ“ (1 / (s - 1) - 1 / (s - 1) * 0) by simp]
    simp_rw [mul_sub, mul_one]
    refine tendsto_const_nhds.sub (Tendsto.const_mul _ ?_)
    refine tendsto_const_nhds.div_atTop <| (tendsto_rpow_atTop (by linarith)).comp ?_
    exact tendsto_atTop_add_const_right _ _ tendsto_natCast_atTop_atTop
  Â· rw [â† sub_zero (tsum _)]
    apply (((Summable.hasSum ?_).tendsto_sum_nat).sub ?_).const_mul
    Â· exact_mod_cast (summable_nat_add_iff 1).mpr (summable_one_div_nat_rpow.mpr hs)
    Â· apply tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds
      Â· change Tendsto (fun n : â„• â†¦ (1 / â†‘(n + 1) : â„) ^ (s - 1)) ..
        rw [show ğ“ (0 : â„) = ğ“ (0 ^ (s - 1)) by rw [zero_rpow]; linarith]
        refine Tendsto.rpow_const ?_ (Or.inr <| by linarith)
        exact (tendsto_const_div_atTop_nhds_zero_nat _).comp (tendsto_add_atTop_nat _)
      Â· intro n
        positivity
      Â· intro n
        dsimp only
        transitivity (n + 1) / (n + 1) ^ s
        Â· gcongr
          linarith
        Â· apply le_of_eq
          rw [rpow_sub_one, â† div_mul, div_one, mul_comm, one_div, inv_rpow, â† div_eq_mul_inv]
          Â· norm_cast
          all_goals positivity

/-- Reformulation of `ZetaAsymptotics.term_tsum_of_lt` which is useful for some computations
below. -/
lemma zeta_limit_aux1 {s : â„} (hs : 1 < s) :
    (âˆ‘' n : â„•, 1 / (n + 1 : â„) ^ s) - 1 / (s - 1) = 1 - s * term_tsum s := by
  rw [term_tsum_of_lt hs]
  generalize (âˆ‘' n : â„•, 1 / (n + 1 : â„) ^ s) = Z
  field_simp [(show s - 1 â‰  0 by linarith)]
  ring_nf

end s_gt_one

section continuity

/-!
## Continuity of the sum
-/

lemma continuousOn_term (n : â„•) :
    ContinuousOn (fun x â†¦ term (n + 1) x) (Ici 1) := by
  -- TODO: can this be shortened using the lemma
  -- `continuous_parametric_intervalIntegral_of_continuous'` from #11185?
  simp only [term, intervalIntegral.integral_of_le (by linarith : (â†‘(n + 1) : â„) â‰¤ â†‘(n + 1) + 1)]
  apply continuousOn_of_dominated (bound := fun x â†¦ (x - â†‘(n + 1)) / x ^ (2 : â„))
  Â· exact fun s hs â†¦ (term_welldef (by simp) (zero_lt_one.trans_le hs)).1.1
  Â· intro s (hs : 1 â‰¤ s)
    rw [ae_restrict_iff' measurableSet_Ioc]
    filter_upwards with x hx
    have : 0 < x := lt_trans (by positivity) hx.1
    rw [norm_of_nonneg (div_nonneg (sub_nonneg.mpr hx.1.le) (by positivity)), Nat.cast_add_one]
    apply div_le_div_of_nonneg_left
    Â· exact_mod_cast sub_nonneg.mpr hx.1.le
    Â· positivity
    Â· exact rpow_le_rpow_of_exponent_le (le_trans (by simp) hx.1.le) (by linarith)
  Â· rw [â† IntegrableOn, â† intervalIntegrable_iff_integrableOn_Ioc_of_le (by linarith)]
    exact_mod_cast term_welldef (by linarith : 0 < (n + 1)) zero_lt_one
  Â· rw [ae_restrict_iff' measurableSet_Ioc]
    filter_upwards with x hx
    refine ContinuousAt.continuousOn (fun s (hs : 1 â‰¤ s) â†¦ continuousAt_const.div ?_ ?_)
    Â· exact continuousAt_const.rpow (continuousAt_id.add continuousAt_const) (Or.inr (by linarith))
    Â· exact (rpow_pos_of_pos ((Nat.cast_pos.mpr (by simp)).trans hx.1) _).ne'

lemma continuousOn_term_tsum : ContinuousOn term_tsum (Ici 1) := by
  -- We use dominated convergence, using `fun n â†¦ term n 1` as our uniform bound (since `term` is
  -- monotone decreasing in `s`.)
  refine continuousOn_tsum (fun i â†¦ continuousOn_term _) term_tsum_one.summable (fun n s hs â†¦ ?_)
  rw [term, term, norm_of_nonneg]
  Â· simp_rw [intervalIntegral.integral_of_le (by linarith : (â†‘(n + 1) : â„) â‰¤ â†‘(n + 1) + 1)]
    refine setIntegral_mono_on ?_ ?_ measurableSet_Ioc (fun x hx â†¦ ?_)
    Â· exact (term_welldef n.succ_pos (zero_lt_one.trans_le hs)).1
    Â· exact (term_welldef n.succ_pos zero_lt_one).1
    Â· rw [div_le_div_left] -- leave side-goals to end and kill them all together
      Â· apply rpow_le_rpow_of_exponent_le
        Â· exact (lt_of_le_of_lt (by simp) hx.1).le
        Â· linarith [mem_Ici.mp hs]
      Â· linarith [hx.1]
      all_goals apply rpow_pos_of_pos ((Nat.cast_nonneg _).trans_lt hx.1)
  Â· rw [intervalIntegral.integral_of_le (by linarith)]
    refine setIntegral_nonneg measurableSet_Ioc (fun x hx â†¦ div_nonneg ?_ (rpow_nonneg ?_ _))
    all_goals linarith [hx.1]

/-- First version of the limit formula, with a limit over real numbers tending to 1 from above. -/
lemma tendsto_riemannZeta_sub_one_div_nhds_right :
    Tendsto (fun s : â„ â†¦ riemannZeta s - 1 / (s - 1)) (ğ“[>] 1) (ğ“ Î³) := by
  suffices Tendsto (fun s : â„ â†¦ (âˆ‘' n : â„•, 1 / (n + 1 : â„) ^ s) - 1 / (s - 1))
    (ğ“[>] 1) (ğ“ Î³) by
    apply ((Complex.continuous_ofReal.tendsto _).comp this).congr'
    filter_upwards [self_mem_nhdsWithin] with s hs
    simp only [Function.comp_apply, Complex.ofReal_sub, Complex.ofReal_div,
      Complex.ofReal_one, sub_left_inj, Complex.ofReal_tsum]
    rw [zeta_eq_tsum_one_div_nat_add_one_cpow (by simpa using hs)]
    congr 1 with n
    rw [Complex.ofReal_cpow (by positivity)]
    norm_cast
  suffices aux2 : Tendsto (fun s : â„ â†¦ (âˆ‘' n : â„•, 1 / (n + 1 : â„) ^ s) - 1 / (s - 1))
    (ğ“[>] 1) (ğ“ (1 - term_tsum 1)) by
    have := term_tsum_one.tsum_eq
    rw [â† term_tsum, eq_sub_iff_add_eq, â† eq_sub_iff_add_eq'] at this
    simpa only [this] using aux2
  apply Tendsto.congr'
  Â· filter_upwards [self_mem_nhdsWithin] with s hs using (zeta_limit_aux1 hs).symm
  Â· apply tendsto_const_nhds.sub
    rw [â† one_mul (term_tsum 1)]
    apply (tendsto_id.mono_left nhdsWithin_le_nhds).mul
    have := continuousOn_term_tsum.continuousWithinAt left_mem_Ici
    exact Tendsto.mono_left this (nhdsWithin_mono _ Ioi_subset_Ici_self)

/-- The function `Î¶ s - 1 / (s - 1)` tends to `Î³` as `s â†’ 1`. -/
theorem _root_.tendsto_riemannZeta_sub_one_div :
    Tendsto (fun s : â„‚ â†¦ riemannZeta s - 1 / (s - 1)) (ğ“[â‰ ] 1) (ğ“ Î³) := by
  -- We use the removable-singularity theorem to show that *some* limit over `ğ“[â‰ ] (1 : â„‚)` exists,
  -- and then use the previous result to deduce that this limit must be `Î³`.
  let f (s : â„‚) := riemannZeta s - 1 / (s - 1)
  suffices âˆƒ C, Tendsto f (ğ“[â‰ ] 1) (ğ“ C) by
    cases' this with C hC
    suffices Tendsto (fun s : â„ â†¦ f s) _ _
      from (tendsto_nhds_unique this tendsto_riemannZeta_sub_one_div_nhds_right) â–¸ hC
    refine hC.comp (tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _ ?_ ?_)
    Â· exact (Complex.continuous_ofReal.tendsto 1).mono_left (nhdsWithin_le_nhds ..)
    Â· filter_upwards [self_mem_nhdsWithin] with a ha
      rw [mem_compl_singleton_iff, â† Complex.ofReal_one, Ne, Complex.ofReal_inj]
      exact ne_of_gt ha
  refine âŸ¨_, Complex.tendsto_limUnder_of_differentiable_on_punctured_nhds_of_isLittleO ?_ ?_âŸ©
  Â· filter_upwards [self_mem_nhdsWithin] with s hs
    refine (differentiableAt_riemannZeta hs).sub ((differentiableAt_const _).div ?_ ?_)
    Â· fun_prop
    Â· rwa [mem_compl_singleton_iff, â† sub_ne_zero] at hs
  Â· refine Asymptotics.isLittleO_of_tendsto' ?_ ?_
    Â· filter_upwards [self_mem_nhdsWithin] with t ht ht'
      rw [inv_eq_zero, sub_eq_zero] at ht'
      tauto
    Â· simp_rw [div_eq_mul_inv, inv_inv, sub_mul,
        (by ring_nf : ğ“ (0 : â„‚) = ğ“ ((1 - 1) - f 1 * (1 - 1)))]
      apply Tendsto.sub
      Â· simp_rw [mul_comm (f _), f, mul_sub]
        apply riemannZeta_residue_one.sub
        refine Tendsto.congr' ?_ (tendsto_const_nhds.mono_left nhdsWithin_le_nhds)
        filter_upwards [self_mem_nhdsWithin] with x hx
        field_simp [sub_ne_zero.mpr <| mem_compl_singleton_iff.mp hx]
      Â· exact ((tendsto_id.sub tendsto_const_nhds).mono_left nhdsWithin_le_nhds).const_mul _

lemma _root_.isBigO_riemannZeta_sub_one_div {F : Type*} [Norm F] [One F] [NormOneClass F] :
    (fun s : â„‚ â†¦ riemannZeta s - 1 / (s - 1)) =O[ğ“ 1] (fun _ â†¦ 1 : â„‚ â†’ F) := by
  simpa only [Asymptotics.isBigO_one_nhds_ne_iff] using
     tendsto_riemannZeta_sub_one_div.isBigO_one (F := F)

end continuity

section val_at_one

open Complex

lemma tendsto_Gamma_term_aux : Tendsto (fun s â†¦ 1 / (s - 1) - 1 / Gammaâ„ s / (s - 1)) (ğ“[â‰ ] 1)
    (ğ“ (-(Î³ + Complex.log (4 * â†‘Ï€)) / 2)) := by
  have h := hasDerivAt_Gammaâ„_one
  rw [hasDerivAt_iff_tendsto_slope, slope_fun_def_field, Gammaâ„_one] at h
  have := h.div (hasDerivAt_Gammaâ„_one.continuousAt.tendsto.mono_left nhdsWithin_le_nhds)
    (Gammaâ„_one.trans_ne one_ne_zero)
  rw [Gammaâ„_one, div_one] at this
  refine this.congr' ?_
  have : {z | 0 < re z} âˆˆ ğ“ (1 : â„‚) := by
    apply (continuous_re.isOpen_preimage _ isOpen_Ioi).mem_nhds
    simp only [mem_preimage, one_re, mem_Ioi, zero_lt_one]
  rw [EventuallyEq, eventually_nhdsWithin_iff]
  filter_upwards [this] with a ha _
  rw [Pi.div_apply, â† sub_div, div_right_comm, sub_div' _ _ _ (Gammaâ„_ne_zero_of_re_pos ha),
    one_mul]

lemma tendsto_riemannZeta_sub_one_div_Gammaâ„ :
    Tendsto (fun s â†¦ riemannZeta s - 1 / Gammaâ„ s / (s - 1)) (ğ“[â‰ ] 1)
    (ğ“ ((Î³ - Complex.log (4 * â†‘Ï€)) / 2)) := by
  have := tendsto_riemannZeta_sub_one_div.add tendsto_Gamma_term_aux
  simp_rw [sub_add_sub_cancel] at this
  convert this using 2
  ring_nf

/-- Formula for `Î¶ 1`. Note that mathematically `Î¶ 1` is undefined, but our construction ascribes
this particular value to it. -/
lemma _root_.riemannZeta_one : riemannZeta 1 = (Î³ - Complex.log (4 * â†‘Ï€)) / 2 := by
  have := (HurwitzZeta.tendsto_hurwitzZetaEven_sub_one_div_nhds_one 0).mono_left
    <| nhdsWithin_le_nhds (s := {1}á¶œ)
  simp only [HurwitzZeta.hurwitzZetaEven_zero, div_right_comm _ _ (Gammaâ„ _)] at this
  exact tendsto_nhds_unique this tendsto_riemannZeta_sub_one_div_Gammaâ„

/-- Formula for `Î› 1`. Note that mathematically `Î› 1` is undefined, but our construction ascribes
this particular value to it. -/
lemma _root_.completedRiemannZeta_one :
    completedRiemannZeta 1 = (Î³ - Complex.log (4 * â†‘Ï€)) / 2 :=
  (riemannZeta_one â–¸ div_one (_ : â„‚) â–¸ Gammaâ„_one â–¸ riemannZeta_def_of_ne_zero one_ne_zero).symm

/-- Formula for `Î›â‚€ 1`, where `Î›â‚€` is the entire function satisfying
`Î›â‚€ s = Ï€ ^ (-s / 2) Î“(s / 2) Î¶(s) + 1 / s + 1 / (1 - s)` away from `s = 0, 1`.

Note that `s = 1` is _not_ a pole of `Î›â‚€`, so this statement (unlike `riemannZeta_one`) is
a mathematically meaningful statement and is not dependent on Mathlib's particular conventions for
division by zero. -/
lemma _root_.completedRiemannZetaâ‚€_one :
    completedRiemannZetaâ‚€ 1 = (Î³ - Complex.log (4 * â†‘Ï€)) / 2 + 1 := by
  have := completedRiemannZeta_eq 1
  rw [sub_self, div_zero, div_one, sub_zero, eq_sub_iff_add_eq] at this
  rw [â† this, completedRiemannZeta_one]

/-- With Mathlib's particular conventions, we have `Î¶ 1 â‰  0`. -/
lemma _root_.riemannZeta_one_ne_zero : riemannZeta 1 â‰  0 := by
  -- This one's for you, Kevin.
  suffices (Î³ - (4 * Ï€).log) / 2 â‰  0 by
    simpa only [riemannZeta_one, â† ofReal_ne_zero, ofReal_log (by positivity : 0 â‰¤ 4 * Ï€),
      push_cast]
  refine div_ne_zero (sub_lt_zero.mpr (lt_trans ?_ ?_ (b := 1))).ne two_ne_zero
  Â· exact Real.eulerMascheroniConstant_lt_two_thirds.trans (by norm_num)
  Â· rw [lt_log_iff_exp_lt (by positivity)]
    exact (lt_trans Real.exp_one_lt_d9 (by norm_num)).trans_le
      <| mul_le_mul_of_nonneg_left two_le_pi (by norm_num)

end val_at_one

end ZetaAsymptotics
