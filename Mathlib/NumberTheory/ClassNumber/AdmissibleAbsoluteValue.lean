/-
Copyright (c) 2021 Anne Baanen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anne Baanen
-/
import Mathlib.Data.Real.Basic
import Mathlib.Combinatorics.Pigeonhole
import Mathlib.Algebra.Order.AbsoluteValue.Euclidean

/-!
# Admissible absolute values
This file defines a structure `AbsoluteValue.IsAdmissible` which we use to show the class number
of the ring of integers of a global field is finite.

## Main definitions

* `AbsoluteValue.IsAdmissible abv` states the absolute value `abv : R ‚Üí ‚Ñ§`
  respects the Euclidean domain structure on `R`, and that a large enough set
  of elements of `R^n` contains a pair of elements whose remainders are
  pointwise close together.

## Main results

* `AbsoluteValue.absIsAdmissible` shows the "standard" absolute value on `‚Ñ§`,
  mapping negative `x` to `-x`, is admissible.
* `Polynomial.cardPowDegreeIsAdmissible` shows `cardPowDegree`,
  mapping `p : Polynomial ùîΩ_q` to `q ^ degree p`, is admissible
-/

local infixl:50 " ‚â∫ " => EuclideanDomain.r

namespace AbsoluteValue

variable {R : Type*} [EuclideanDomain R]
variable (abv : AbsoluteValue R ‚Ñ§)

/-- An absolute value `R ‚Üí ‚Ñ§` is admissible if it respects the Euclidean domain
structure and a large enough set of elements in `R^n` will contain a pair of
elements whose remainders are pointwise close together. -/
structure IsAdmissible extends IsEuclidean abv where
  /-- The cardinality required for a given `Œµ`. -/
  protected card : ‚Ñù ‚Üí ‚Ñï
  /-- For all `Œµ > 0` and finite families `A`, we can partition the remainders of `A` mod `b`
  into `abv.card Œµ` sets, such that all elements in each part of remainders are close together. -/
  exists_partition' :
    ‚àÄ (n : ‚Ñï) {Œµ : ‚Ñù} (_ : 0 < Œµ) {b : R} (_ : b ‚â† 0) (A : Fin n ‚Üí R),
      ‚àÉ t : Fin n ‚Üí Fin (card Œµ), ‚àÄ i‚ÇÄ i‚ÇÅ, t i‚ÇÄ = t i‚ÇÅ ‚Üí (abv (A i‚ÇÅ % b - A i‚ÇÄ % b) : ‚Ñù) < abv b ‚Ä¢ Œµ

namespace IsAdmissible

variable {abv}

/-- For all `Œµ > 0` and finite families `A`, we can partition the remainders of `A` mod `b`
into `abv.card Œµ` sets, such that all elements in each part of remainders are close together. -/
theorem exists_partition {Œπ : Type*} [Finite Œπ] {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) {b : R} (hb : b ‚â† 0)
    (A : Œπ ‚Üí R) (h : abv.IsAdmissible) : ‚àÉ t : Œπ ‚Üí Fin (h.card Œµ),
      ‚àÄ i‚ÇÄ i‚ÇÅ, t i‚ÇÄ = t i‚ÇÅ ‚Üí (abv (A i‚ÇÅ % b - A i‚ÇÄ % b) : ‚Ñù) < abv b ‚Ä¢ Œµ := by
  rcases Finite.exists_equiv_fin Œπ with ‚ü®n, ‚ü®e‚ü©‚ü©
  obtain ‚ü®t, ht‚ü© := h.exists_partition' n hŒµ hb (A ‚àò e.symm)
  refine ‚ü®t ‚àò e, fun i‚ÇÄ i‚ÇÅ h ‚Ü¶ ?_‚ü©
  convert (config := {transparency := .default})
    ht (e i‚ÇÄ) (e i‚ÇÅ) h <;> simp only [e.symm_apply_apply]

/-- Any large enough family of vectors in `R^n` has a pair of elements
whose remainders are close together, pointwise. -/
theorem exists_approx_aux (n : ‚Ñï) (h : abv.IsAdmissible) :
    ‚àÄ {Œµ : ‚Ñù} (_hŒµ : 0 < Œµ) {b : R} (_hb : b ‚â† 0) (A : Fin (h.card Œµ ^ n).succ ‚Üí Fin n ‚Üí R),
      ‚àÉ i‚ÇÄ i‚ÇÅ, i‚ÇÄ ‚â† i‚ÇÅ ‚àß ‚àÄ k, (abv (A i‚ÇÅ k % b - A i‚ÇÄ k % b) : ‚Ñù) < abv b ‚Ä¢ Œµ := by
  haveI := Classical.decEq R
  induction n with
  | zero =>
    intro Œµ _hŒµ b _hb A
    refine ‚ü®0, 1, ?_, ?_‚ü©
    ¬∑ simp
    rintro ‚ü®i, ‚ü®‚ü©‚ü©
  | succ n ih =>
  intro Œµ hŒµ b hb A
  let M := h.card Œµ
  -- By the "nicer" pigeonhole principle, we can find a collection `s`
  -- of more than `M^n` remainders where the first components lie close together:
  obtain ‚ü®s, s_inj, hs‚ü© :
    ‚àÉ s : Fin (M ^ n).succ ‚Üí Fin (M ^ n.succ).succ,
      Function.Injective s ‚àß ‚àÄ i‚ÇÄ i‚ÇÅ, (abv (A (s i‚ÇÅ) 0 % b - A (s i‚ÇÄ) 0 % b) : ‚Ñù) < abv b ‚Ä¢ Œµ := by
    -- We can partition the `A`s into `M` subsets where
    -- the first components lie close together:
    obtain ‚ü®t, ht‚ü© :
      ‚àÉ t : Fin (M ^ n.succ).succ ‚Üí Fin M,
        ‚àÄ i‚ÇÄ i‚ÇÅ, t i‚ÇÄ = t i‚ÇÅ ‚Üí (abv (A i‚ÇÅ 0 % b - A i‚ÇÄ 0 % b) : ‚Ñù) < abv b ‚Ä¢ Œµ :=
      h.exists_partition hŒµ hb fun x ‚Ü¶ A x 0
    -- Since the `M` subsets contain more than `M * M^n` elements total,
    -- there must be a subset that contains more than `M^n` elements.
    obtain ‚ü®s, hs‚ü© :=
      Fintype.exists_lt_card_fiber_of_mul_lt_card (f := t)
        (by simpa only [Fintype.card_fin, pow_succ'] using Nat.lt_succ_self (M ^ n.succ))
    refine ‚ü®fun i ‚Ü¶ (Finset.univ.filter fun x ‚Ü¶ t x = s).toList.get <| i.castLE ?_, fun i j h ‚Ü¶ ?_,
      fun i‚ÇÄ i‚ÇÅ ‚Ü¶ ht _ _ ?_‚ü©
    ¬∑ rwa [Finset.length_toList]
    ¬∑ ext
      simpa [(Finset.nodup_toList _).getElem_inj_iff] using h
    ¬∑ #adaptation_note /-- https://github.com/leanprover/lean4/pull/4400
      This proof was nicer before.
      Please feel welcome to improve it, by avoiding use of `List.get` in favour of `GetElem`. -/
      have : ‚àÄ i h, t ((Finset.univ.filter fun x ‚Ü¶ t x = s).toList.get ‚ü®i, h‚ü©) = s := fun i h ‚Ü¶
        (Finset.mem_filter.mp (Finset.mem_toList.mp (List.get_mem _ ‚ü®i, h‚ü©))).2
      simp only [Nat.succ_eq_add_one, Finset.length_toList, List.get_eq_getElem] at this
      simp only [Nat.succ_eq_add_one, List.get_eq_getElem, Fin.coe_castLE]
      rw [this _ (Nat.lt_of_le_of_lt (Nat.le_of_lt_succ i‚ÇÅ.2) hs),
        this _ (Nat.lt_of_le_of_lt (Nat.le_of_lt_succ i‚ÇÄ.2) hs)]
  -- Since `s` is large enough, there are two elements of `A ‚àò s`
  -- where the second components lie close together.
  obtain ‚ü®k‚ÇÄ, k‚ÇÅ, hk, h‚ü© := ih hŒµ hb fun x ‚Ü¶ Fin.tail (A (s x))
  refine ‚ü®s k‚ÇÄ, s k‚ÇÅ, fun h ‚Ü¶ hk (s_inj h), fun i ‚Ü¶ Fin.cases ?_ (fun i ‚Ü¶ ?_) i‚ü©
  ¬∑ exact hs k‚ÇÄ k‚ÇÅ
  ¬∑ exact h i

/-- Any large enough family of vectors in `R^Œπ` has a pair of elements
whose remainders are close together, pointwise. -/
theorem exists_approx {Œπ : Type*} [Fintype Œπ] {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) {b : R} (hb : b ‚â† 0)
    (h : abv.IsAdmissible) (A : Fin (h.card Œµ ^ Fintype.card Œπ).succ ‚Üí Œπ ‚Üí R) :
    ‚àÉ i‚ÇÄ i‚ÇÅ, i‚ÇÄ ‚â† i‚ÇÅ ‚àß ‚àÄ k, (abv (A i‚ÇÅ k % b - A i‚ÇÄ k % b) : ‚Ñù) < abv b ‚Ä¢ Œµ := by
  let e := Fintype.equivFin Œπ
  obtain ‚ü®i‚ÇÄ, i‚ÇÅ, ne, h‚ü© := h.exists_approx_aux (Fintype.card Œπ) hŒµ hb fun x y ‚Ü¶ A x (e.symm y)
  refine ‚ü®i‚ÇÄ, i‚ÇÅ, ne, fun k ‚Ü¶ ?_‚ü©
  convert h (e k) <;> simp only [e.symm_apply_apply]

end IsAdmissible

end AbsoluteValue
