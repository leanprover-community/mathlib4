/-
Copyright (c) 2022 Tian Chen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Tian Chen, Mantas BakÅ¡ys
-/
import Mathlib.Algebra.GeomSum
import Mathlib.Data.Int.Parity
import Mathlib.Data.ZMod.Basic
import Mathlib.NumberTheory.Padics.PadicVal
import Mathlib.RingTheory.Ideal.QuotientOperations
import Mathlib.Init.Meta.WellFoundedTactics

#align_import number_theory.multiplicity from "leanprover-community/mathlib"@"e8638a0fcaf73e4500469f368ef9494e495099b3"

/-!
# Multiplicity in Number Theory

This file contains results in number theory relating to multiplicity.

## Main statements

* `multiplicity.Int.pow_sub_pow` is the lifting the exponent lemma for odd primes.
  We also prove several variations of the lemma.

## References

* [Wikipedia, *Lifting-the-exponent lemma*]
  (https://en.wikipedia.org/wiki/Lifting-the-exponent_lemma)
-/


open Ideal Ideal.Quotient Finset

open BigOperators

variable {R : Type*} {n : â„•}

section CommRing

variable [CommRing R] {a b x y : R}

theorem dvd_geom_sumâ‚‚_iff_of_dvd_sub {x y p : R} (h : p âˆ£ x - y) :
    (p âˆ£ âˆ‘ i in range n, x ^ i * y ^ (n - 1 - i)) â†” p âˆ£ n * y ^ (n - 1) := by
  rw [â† mem_span_singleton, â† Ideal.Quotient.eq] at h
  -- âŠ¢ p âˆ£ âˆ‘ i in range n, x ^ i * y ^ (n - 1 - i) â†” p âˆ£ â†‘n * y ^ (n - 1)
  simp only [â† mem_span_singleton, â† eq_zero_iff_mem, RingHom.map_geom_sumâ‚‚, h, geom_sumâ‚‚_self,
    _root_.map_mul, map_pow, map_natCast]
#align dvd_geom_sumâ‚‚_iff_of_dvd_sub dvd_geom_sumâ‚‚_iff_of_dvd_sub

theorem dvd_geom_sumâ‚‚_iff_of_dvd_sub' {x y p : R} (h : p âˆ£ x - y) :
    (p âˆ£ âˆ‘ i in range n, x ^ i * y ^ (n - 1 - i)) â†” p âˆ£ n * x ^ (n - 1) := by
  rw [geom_sumâ‚‚_comm, dvd_geom_sumâ‚‚_iff_of_dvd_sub]; simpa using h.neg_right
  -- âŠ¢ p âˆ£ y - x
                                                     -- ğŸ‰ no goals
#align dvd_geom_sumâ‚‚_iff_of_dvd_sub' dvd_geom_sumâ‚‚_iff_of_dvd_sub'

theorem dvd_geom_sumâ‚‚_self {x y : R} (h : â†‘n âˆ£ x - y) :
    â†‘n âˆ£ âˆ‘ i in range n, x ^ i * y ^ (n - 1 - i) :=
  (dvd_geom_sumâ‚‚_iff_of_dvd_sub h).mpr (dvd_mul_right _ _)
#align dvd_geom_sumâ‚‚_self dvd_geom_sumâ‚‚_self

theorem sq_dvd_add_pow_sub_sub (p x : R) (n : â„•) :
    p ^ 2 âˆ£ (x + p) ^ n - x ^ (n - 1) * p * n - x ^ n := by
  cases' n with n n
  -- âŠ¢ p ^ 2 âˆ£ (x + p) ^ Nat.zero - x ^ (Nat.zero - 1) * p * â†‘Nat.zero - x ^ Nat.zero
  Â· simp only [pow_zero, Nat.cast_zero, sub_zero, sub_self, dvd_zero, Nat.zero_eq, mul_zero]
    -- ğŸ‰ no goals
  Â· simp only [Nat.succ_sub_succ_eq_sub, tsub_zero, Nat.cast_succ, add_pow, Finset.sum_range_succ,
      Nat.choose_self, Nat.succ_sub _, tsub_self, pow_one, Nat.choose_succ_self_right, pow_zero,
      mul_one, Nat.cast_zero, zero_add, Nat.succ_eq_add_one, add_tsub_cancel_left]
    suffices p ^ 2 âˆ£ âˆ‘ i : â„• in range n, x ^ i * p ^ (n + 1 - i) * â†‘((n + 1).choose i) by
      convert this; abel
    Â· apply Finset.dvd_sum
      -- âŠ¢ âˆ€ (x_1 : â„•), x_1 âˆˆ range n â†’ p ^ 2 âˆ£ x ^ x_1 * p ^ (n + 1 - x_1) * â†‘(Nat.cho â€¦
      intro y hy
      -- âŠ¢ p ^ 2 âˆ£ x ^ y * p ^ (n + 1 - y) * â†‘(Nat.choose (n + 1) y)
      calc
        p ^ 2 âˆ£ p ^ (n + 1 - y) :=
          pow_dvd_pow p (le_tsub_of_add_le_left (by linarith [Finset.mem_range.mp hy]))
        _ âˆ£ x ^ y * p ^ (n + 1 - y) * â†‘((n + 1).choose y) :=
          dvd_mul_of_dvd_left (dvd_mul_left _ _) _
#align sq_dvd_add_pow_sub_sub sq_dvd_add_pow_sub_sub

theorem not_dvd_geom_sumâ‚‚ {p : R} (hp : Prime p) (hxy : p âˆ£ x - y) (hx : Â¬p âˆ£ x) (hn : Â¬p âˆ£ n) :
    Â¬p âˆ£ âˆ‘ i in range n, x ^ i * y ^ (n - 1 - i) := fun h =>
  hx <|
    hp.dvd_of_dvd_pow <| (hp.dvd_or_dvd <| (dvd_geom_sumâ‚‚_iff_of_dvd_sub' hxy).mp h).resolve_left hn
#align not_dvd_geom_sumâ‚‚ not_dvd_geom_sumâ‚‚

variable {p : â„•} (a b)

theorem odd_sq_dvd_geom_sumâ‚‚_sub (hp : Odd p) :
    (p : R) ^ 2 âˆ£ (âˆ‘ i in range p, (a + p * b) ^ i * a ^ (p - 1 - i)) - p * a ^ (p - 1) := by
  have h1 : âˆ€ (i : â„•),
      (p : R) ^ 2 âˆ£ (a + â†‘p * b) ^ i - (a ^ (i - 1) * (â†‘p * b) * i + a ^ i) := by
    intro i
    calc
      â†‘p ^ 2 âˆ£ (â†‘p * b) ^ 2 := by simp only [mul_pow, dvd_mul_right]
      _ âˆ£ (a + â†‘p * b) ^ i - (a ^ (i - 1) * (â†‘p * b) * â†‘i + a ^ i) := by
        simp only [sq_dvd_add_pow_sub_sub (â†‘p * b) a i, â† sub_sub]
  simp_rw [â† mem_span_singleton, â† Ideal.Quotient.eq] at *
  -- âŠ¢ â†‘(Ideal.Quotient.mk (span {â†‘p ^ 2})) (âˆ‘ i in range p, (a + â†‘p * b) ^ i * a ^ â€¦
  let s : R := (p : R)^2
  -- âŠ¢ â†‘(Ideal.Quotient.mk (span {â†‘p ^ 2})) (âˆ‘ i in range p, (a + â†‘p * b) ^ i * a ^ â€¦
  calc
    (Ideal.Quotient.mk (span {s})) (âˆ‘ i in range p, (a + (p : R) * b) ^ i * a ^ (p - 1 - i)) =
        âˆ‘ i : â„• in Finset.range p,
        mk (span {s}) ((a ^ (i - 1) * (â†‘p * b) * â†‘i + a ^ i) * a ^ (p - 1 - i)) :=
      by simp_rw [RingHom.map_geom_sumâ‚‚, â† map_pow, h1, â† _root_.map_mul]
    _ =
        mk (span {s})
            (âˆ‘ x : â„• in Finset.range p, a ^ (x - 1) * (a ^ (p - 1 - x) * (â†‘p * (b * â†‘x)))) +
          mk (span {s}) (âˆ‘ x : â„• in Finset.range p, a ^ (x + (p - 1 - x))) := by
      ring_nf
      simp only [â† pow_add, map_add, Finset.sum_add_distrib, â† map_sum]
      congr
      simp [pow_add a, mul_assoc]
    _ =
        mk (span {s})
            (âˆ‘ x : â„• in Finset.range p, a ^ (x - 1) * (a ^ (p - 1 - x) * (â†‘p * (b * â†‘x)))) +
          mk (span {s}) (âˆ‘ x : â„• in Finset.range p, a ^ (p - 1)) := by
      rw [add_right_inj]
      have : âˆ€ (x : â„•), (hx : x âˆˆ range p) â†’ a ^ (x + (p - 1 - x)) = a ^ (p - 1) := by
        intro x hx
        rw [â† Nat.add_sub_assoc _ x, Nat.add_sub_cancel_left]
        exact Nat.le_pred_of_lt (Finset.mem_range.mp hx)
      rw [Finset.sum_congr rfl this]
    _ =
        mk (span {s})
            (âˆ‘ x : â„• in Finset.range p, a ^ (x - 1) * (a ^ (p - 1 - x) * (â†‘p * (b * â†‘x)))) +
          mk (span {s}) (â†‘p * a ^ (p - 1)) :=
      by simp only [add_right_inj, Finset.sum_const, Finset.card_range, nsmul_eq_mul]
    _ =
        mk (span {s}) (â†‘p * b * âˆ‘ x : â„• in Finset.range p, a ^ (p - 2) * x) +
          mk (span {s}) (â†‘p * a ^ (p - 1)) := by
      simp only [Finset.mul_sum, â† mul_assoc, â† pow_add]
      rw [Finset.sum_congr rfl]
      rintro (âŸ¨âŸ© | âŸ¨xâŸ©) hx
      Â· rw [Nat.cast_zero, mul_zero, mul_zero]
      Â· have : x.succ - 1 + (p - 1 - x.succ) = p - 2 := by
          rw [â† Nat.add_sub_assoc (Nat.le_pred_of_lt (Finset.mem_range.mp hx))]
          exact congr_arg Nat.pred (Nat.add_sub_cancel_left _ _)
        rw [this]
        ring1
    _ = mk (span {s}) (â†‘p * a ^ (p - 1)) := by
      have : Finset.sum (range p) (fun (x : â„•) â†¦ (x : R)) =
          ((Finset.sum (range p) (fun (x : â„•) â†¦ (x : â„•)))) := by simp only [Nat.cast_sum]
      simp only [add_left_eq_self, â† Finset.mul_sum, this]
      norm_cast
      simp only [Finset.sum_range_id]
      norm_cast
      simp only [Nat.cast_mul, _root_.map_mul,
          Nat.mul_div_assoc p (even_iff_two_dvd.mp (Nat.Odd.sub_odd hp odd_one))]
      ring_nf
      rw [mul_assoc, mul_assoc]
      refine' mul_eq_zero_of_left _ _
      refine' Ideal.Quotient.eq_zero_iff_mem.mpr _
      simp [mem_span_singleton]
#align odd_sq_dvd_geom_sumâ‚‚_sub odd_sq_dvd_geom_sumâ‚‚_sub

namespace multiplicity

section IntegralDomain

variable [IsDomain R] [@DecidableRel R (Â· âˆ£ Â·)]

theorem pow_sub_pow_of_prime {p : R} (hp : Prime p) {x y : R} (hxy : p âˆ£ x - y) (hx : Â¬p âˆ£ x)
    {n : â„•} (hn : Â¬p âˆ£ n) : multiplicity p (x ^ n - y ^ n) = multiplicity p (x - y) := by
  rw [â† geom_sumâ‚‚_mul, multiplicity.mul hp, multiplicity_eq_zero.2 (not_dvd_geom_sumâ‚‚ hp hxy hx hn),
    zero_add]
#align multiplicity.pow_sub_pow_of_prime multiplicity.pow_sub_pow_of_prime

variable (hp : Prime (p : R)) (hp1 : Odd p) (hxy : â†‘p âˆ£ x - y) (hx : Â¬â†‘p âˆ£ x)

theorem geom_sumâ‚‚_eq_one : multiplicity (â†‘p) (âˆ‘ i in range p, x ^ i * y ^ (p - 1 - i)) = 1 := by
  rw [â† Nat.cast_one]
  -- âŠ¢ multiplicity (â†‘p) (âˆ‘ i in range p, x ^ i * y ^ (p - â†‘1 - i)) = 1
  refine' multiplicity.eq_coe_iff.2 âŸ¨_, _âŸ©
  -- âŠ¢ â†‘p ^ 1 âˆ£ âˆ‘ i in range p, x ^ i * y ^ (p - â†‘1 - i)
  Â· rw [pow_one]
    -- âŠ¢ â†‘p âˆ£ âˆ‘ i in range p, x ^ i * y ^ (p - â†‘1 - i)
    exact dvd_geom_sumâ‚‚_self hxy
    -- ğŸ‰ no goals
  rw [dvd_iff_dvd_of_dvd_sub hxy] at hx
  -- âŠ¢ Â¬â†‘p ^ (1 + 1) âˆ£ âˆ‘ i in range p, x ^ i * y ^ (p - â†‘1 - i)
  cases' hxy with k hk
  -- âŠ¢ Â¬â†‘p ^ (1 + 1) âˆ£ âˆ‘ i in range p, x ^ i * y ^ (p - â†‘1 - i)
  rw [one_add_one_eq_two, eq_add_of_sub_eq' hk]
  -- âŠ¢ Â¬â†‘p ^ 2 âˆ£ âˆ‘ i in range p, (y + â†‘p * k) ^ i * y ^ (p - â†‘1 - i)
  refine' mt (dvd_iff_dvd_of_dvd_sub (@odd_sq_dvd_geom_sumâ‚‚_sub _ _ y k _ hp1)).mp _
  -- âŠ¢ Â¬â†‘p ^ 2 âˆ£ â†‘p * y ^ (p - 1)
  rw [pow_two, mul_dvd_mul_iff_left hp.ne_zero]
  -- âŠ¢ Â¬â†‘p âˆ£ y ^ (p - 1)
  exact mt hp.dvd_of_dvd_pow hx
  -- ğŸ‰ no goals
#align multiplicity.geom_sumâ‚‚_eq_one multiplicity.geom_sumâ‚‚_eq_one

theorem pow_prime_sub_pow_prime :
    multiplicity (â†‘p) (x ^ p - y ^ p) = multiplicity (â†‘p) (x - y) + 1 := by
  rw [â† geom_sumâ‚‚_mul, multiplicity.mul hp, geom_sumâ‚‚_eq_one hp hp1 hxy hx, add_comm]
  -- ğŸ‰ no goals
#align multiplicity.pow_prime_sub_pow_prime multiplicity.pow_prime_sub_pow_prime

theorem pow_prime_pow_sub_pow_prime_pow (a : â„•) :
    multiplicity (â†‘p) (x ^ p ^ a - y ^ p ^ a) = multiplicity (â†‘p) (x - y) + a := by
  induction' a with a h_ind
  -- âŠ¢ multiplicity (â†‘p) (x ^ p ^ Nat.zero - y ^ p ^ Nat.zero) = multiplicity (â†‘p)  â€¦
  Â· rw [Nat.cast_zero, add_zero, pow_zero, pow_one, pow_one]
    -- ğŸ‰ no goals
  rw [â† Nat.add_one, Nat.cast_add, Nat.cast_one, â† add_assoc, â† h_ind, pow_succ', pow_mul, pow_mul]
  -- âŠ¢ multiplicity (â†‘p) ((x ^ p ^ a) ^ p - (y ^ p ^ a) ^ p) = multiplicity (â†‘p) (x â€¦
  apply pow_prime_sub_pow_prime hp hp1
  -- âŠ¢ â†‘p âˆ£ x ^ p ^ a - y ^ p ^ a
  Â· rw [â† geom_sumâ‚‚_mul]
    -- âŠ¢ â†‘p âˆ£ (âˆ‘ i in range (p ^ a), x ^ i * y ^ (p ^ a - 1 - i)) * (x - y)
    exact dvd_mul_of_dvd_right hxy _
    -- ğŸ‰ no goals
  Â· exact fun h => hx (hp.dvd_of_dvd_pow h)
    -- ğŸ‰ no goals
#align multiplicity.pow_prime_pow_sub_pow_prime_pow multiplicity.pow_prime_pow_sub_pow_prime_pow

end IntegralDomain

section LiftingTheExponent

variable (hp : Nat.Prime p) (hp1 : Odd p)

/-- **Lifting the exponent lemma** for odd primes. -/
theorem Int.pow_sub_pow {x y : â„¤} (hxy : â†‘p âˆ£ x - y) (hx : Â¬â†‘p âˆ£ x) (n : â„•) :
    multiplicity (â†‘p) (x ^ n - y ^ n) = multiplicity (â†‘p) (x - y) + multiplicity p n := by
  cases' n with n
  -- âŠ¢ multiplicity (â†‘p) (x ^ Nat.zero - y ^ Nat.zero) = multiplicity (â†‘p) (x - y)  â€¦
  Â· simp only [multiplicity.zero, add_top, pow_zero, sub_self, Nat.zero_eq]
    -- ğŸ‰ no goals
  have h : (multiplicity _ _).Dom := finite_nat_iff.mpr âŸ¨hp.ne_one, n.succ_posâŸ©
  -- âŠ¢ multiplicity (â†‘p) (x ^ Nat.succ n - y ^ Nat.succ n) = multiplicity (â†‘p) (x - â€¦
  rcases eq_coe_iff.mp (PartENat.natCast_get h).symm with âŸ¨âŸ¨k, hkâŸ©, hpnâŸ©
  -- âŠ¢ multiplicity (â†‘p) (x ^ Nat.succ n - y ^ Nat.succ n) = multiplicity (â†‘p) (x - â€¦
  conv_lhs => rw [hk, pow_mul, pow_mul]
  -- âŠ¢ multiplicity (â†‘p) ((x ^ p ^ Part.get (multiplicity p (Nat.succ n)) h) ^ k -  â€¦
  rw [Nat.prime_iff_prime_int] at hp
  -- âŠ¢ multiplicity (â†‘p) ((x ^ p ^ Part.get (multiplicity p (Nat.succ n)) h) ^ k -  â€¦
  rw [pow_sub_pow_of_prime hp, pow_prime_pow_sub_pow_prime_pow hp hp1 hxy hx, PartENat.natCast_get]
  Â· rw [â† geom_sumâ‚‚_mul]
    -- âŠ¢ â†‘p âˆ£ (âˆ‘ i in range (p ^ Part.get (multiplicity p (Nat.succ n)) h), x ^ i * y â€¦
    exact dvd_mul_of_dvd_right hxy _
    -- ğŸ‰ no goals
  Â· exact fun h => hx (hp.dvd_of_dvd_pow h)
    -- ğŸ‰ no goals
  Â· rw [Int.coe_nat_dvd]
    -- âŠ¢ Â¬p âˆ£ k
    rintro âŸ¨c, rflâŸ©
    -- âŠ¢ False
    refine' hpn âŸ¨c, _âŸ©
    -- âŠ¢ Nat.succ n = p ^ (Part.get (multiplicity p (Nat.succ n)) h + 1) * c
    rwa [pow_succ', mul_assoc]
    -- ğŸ‰ no goals
#align multiplicity.int.pow_sub_pow multiplicity.Int.pow_sub_pow

theorem Int.pow_add_pow {x y : â„¤} (hxy : â†‘p âˆ£ x + y) (hx : Â¬â†‘p âˆ£ x) {n : â„•} (hn : Odd n) :
    multiplicity (â†‘p) (x ^ n + y ^ n) = multiplicity (â†‘p) (x + y) + multiplicity p n := by
  rw [â† sub_neg_eq_add] at hxy
  -- âŠ¢ multiplicity (â†‘p) (x ^ n + y ^ n) = multiplicity (â†‘p) (x + y) + multiplicity â€¦
  rw [â† sub_neg_eq_add, â† sub_neg_eq_add, â† Odd.neg_pow hn]
  -- âŠ¢ multiplicity (â†‘p) (x ^ n - (-y) ^ n) = multiplicity (â†‘p) (x - -y) + multipli â€¦
  exact Int.pow_sub_pow hp hp1 hxy hx n
  -- ğŸ‰ no goals
#align multiplicity.int.pow_add_pow multiplicity.Int.pow_add_pow

theorem Nat.pow_sub_pow {x y : â„•} (hxy : p âˆ£ x - y) (hx : Â¬p âˆ£ x) (n : â„•) :
    multiplicity p (x ^ n - y ^ n) = multiplicity p (x - y) + multiplicity p n := by
  obtain hyx | hyx := le_total y x
  -- âŠ¢ multiplicity p (x ^ n - y ^ n) = multiplicity p (x - y) + multiplicity p n
  Â· iterate 2 rw [â† Int.coe_nat_multiplicity]
    -- âŠ¢ multiplicity â†‘p â†‘(x ^ n - y ^ n) = multiplicity â†‘p â†‘(x - y) + multiplicity p n
    rw [Int.ofNat_sub (Nat.pow_le_pow_of_le_left hyx n)]
    -- âŠ¢ multiplicity (â†‘p) (â†‘(x ^ n) - â†‘(y ^ n)) = multiplicity â†‘p â†‘(x - y) + multipl â€¦
    rw [â† Int.coe_nat_dvd] at hxy hx
    -- âŠ¢ multiplicity (â†‘p) (â†‘(x ^ n) - â†‘(y ^ n)) = multiplicity â†‘p â†‘(x - y) + multipl â€¦
    rw [Int.coe_nat_sub hyx] at *
    -- âŠ¢ multiplicity (â†‘p) (â†‘(x ^ n) - â†‘(y ^ n)) = multiplicity (â†‘p) (â†‘x - â†‘y) + mult â€¦
    push_cast at *
    -- âŠ¢ multiplicity (â†‘p) (â†‘x ^ n - â†‘y ^ n) = multiplicity (â†‘p) (â†‘x - â†‘y) + multipli â€¦
    exact Int.pow_sub_pow hp hp1 hxy hx n
    -- ğŸ‰ no goals
  Â· simp only [Nat.sub_eq_zero_iff_le.mpr hyx,
      Nat.sub_eq_zero_iff_le.mpr (Nat.pow_le_pow_of_le_left hyx n), multiplicity.zero,
      PartENat.top_add]
#align multiplicity.nat.pow_sub_pow multiplicity.Nat.pow_sub_pow

theorem Nat.pow_add_pow {x y : â„•} (hxy : p âˆ£ x + y) (hx : Â¬p âˆ£ x) {n : â„•} (hn : Odd n) :
    multiplicity p (x ^ n + y ^ n) = multiplicity p (x + y) + multiplicity p n := by
  iterate 2 rw [â† Int.coe_nat_multiplicity]
  -- âŠ¢ multiplicity â†‘p â†‘(x ^ n + y ^ n) = multiplicity â†‘p â†‘(x + y) + multiplicity p n
  rw [â† Int.coe_nat_dvd] at hxy hx
  -- âŠ¢ multiplicity â†‘p â†‘(x ^ n + y ^ n) = multiplicity â†‘p â†‘(x + y) + multiplicity p n
  push_cast at *
  -- âŠ¢ multiplicity (â†‘p) (â†‘x ^ n + â†‘y ^ n) = multiplicity (â†‘p) (â†‘x + â†‘y) + multipli â€¦
  exact Int.pow_add_pow hp hp1 hxy hx hn
  -- ğŸ‰ no goals
#align multiplicity.nat.pow_add_pow multiplicity.Nat.pow_add_pow

end LiftingTheExponent

end multiplicity

end CommRing

theorem pow_two_pow_sub_pow_two_pow [CommRing R] {x y : R} (n : â„•) :
    x ^ 2 ^ n - y ^ 2 ^ n = (âˆ i in Finset.range n, (x ^ 2 ^ i + y ^ 2 ^ i)) * (x - y) := by
  induction' n with d hd
  -- âŠ¢ x ^ 2 ^ Nat.zero - y ^ 2 ^ Nat.zero = (âˆ i in range Nat.zero, (x ^ 2 ^ i + y â€¦
  Â· simp only [pow_zero, pow_one, range_zero, prod_empty, one_mul, Nat.zero_eq]
    -- ğŸ‰ no goals
  Â· suffices x ^ 2 ^ d.succ - y ^ 2 ^ d.succ = (x ^ 2 ^ d + y ^ 2 ^ d) * (x ^ 2 ^ d - y ^ 2 ^ d) by
      rw [this, hd, Finset.prod_range_succ, â† mul_assoc, mul_comm (x ^ 2 ^ d + y ^ 2 ^ d)]
    Â· rw [Nat.succ_eq_add_one]
      -- âŠ¢ x ^ 2 ^ (d + 1) - y ^ 2 ^ (d + 1) = (x ^ 2 ^ d + y ^ 2 ^ d) * (x ^ 2 ^ d - y â€¦
      ring
      -- ğŸ‰ no goals
#align pow_two_pow_sub_pow_two_pow pow_two_pow_sub_pow_two_pow

-- porting note: simplified proof because `fin_cases` was not available in that case
theorem Int.sq_mod_four_eq_one_of_odd {x : â„¤} : Odd x â†’ x ^ 2 % 4 = 1 := by
  intro hx
  -- âŠ¢ x ^ 2 % 4 = 1
  unfold Odd at hx
  -- âŠ¢ x ^ 2 % 4 = 1
  rcases hx with âŸ¨_, rflâŸ©
  -- âŠ¢ (2 * wâœ + 1) ^ 2 % 4 = 1
  ring_nf
  -- âŠ¢ (1 + wâœ * 4 + wâœ ^ 2 * 4) % 4 = 1
  rw [add_assoc, â† add_mul, Int.add_mul_emod_self]
  -- âŠ¢ 1 % 4 = 1
  norm_num
  -- ğŸ‰ no goals
#align int.sq_mod_four_eq_one_of_odd Int.sq_mod_four_eq_one_of_odd

theorem Int.two_pow_two_pow_add_two_pow_two_pow {x y : â„¤} (hx : Â¬2 âˆ£ x) (hxy : 4 âˆ£ x - y) (i : â„•) :
    multiplicity 2 (x ^ 2 ^ i + y ^ 2 ^ i) = â†‘(1 : â„•) := by
  have hx_odd : Odd x := by rwa [Int.odd_iff_not_even, even_iff_two_dvd]
  -- âŠ¢ multiplicity 2 (x ^ 2 ^ i + y ^ 2 ^ i) = â†‘1
  have hxy_even : Even (x - y) := even_iff_two_dvd.mpr (dvd_trans (by norm_num) hxy)
  -- âŠ¢ multiplicity 2 (x ^ 2 ^ i + y ^ 2 ^ i) = â†‘1
  have hy_odd : Odd y := by simpa using hx_odd.sub_even hxy_even
  -- âŠ¢ multiplicity 2 (x ^ 2 ^ i + y ^ 2 ^ i) = â†‘1
  refine' multiplicity.eq_coe_iff.mpr âŸ¨_, _âŸ©
  -- âŠ¢ 2 ^ 1 âˆ£ x ^ 2 ^ i + y ^ 2 ^ i
  Â· rw [pow_one, â† even_iff_two_dvd]
    -- âŠ¢ Even (x ^ 2 ^ i + y ^ 2 ^ i)
    exact hx_odd.pow.add_odd hy_odd.pow
    -- ğŸ‰ no goals
  cases' i with i
  -- âŠ¢ Â¬2 ^ (1 + 1) âˆ£ x ^ 2 ^ Nat.zero + y ^ 2 ^ Nat.zero
  Â· intro hxy'
    -- âŠ¢ False
    have : 2 * 2 âˆ£ 2 * x := by
      have := dvd_add hxy hxy'
      norm_num at *
      rw [two_mul]
      exact this
    have : 2 âˆ£ x := (mul_dvd_mul_iff_left (by norm_num)).mp this
    -- âŠ¢ False
    contradiction
    -- ğŸ‰ no goals
  suffices âˆ€ x : â„¤, Odd x â†’ x ^ 2 ^ (i + 1) % 4 = 1 by
    rw [show (2 ^ (1 + 1) : â„¤) = 4 by norm_num, Int.dvd_iff_emod_eq_zero, Int.add_emod,
      this _ hx_odd, this _ hy_odd]
    norm_num
  intro x hx
  -- âŠ¢ x ^ 2 ^ (i + 1) % 4 = 1
  rw [pow_succ, mul_comm, pow_mul, Int.sq_mod_four_eq_one_of_odd hx.pow]
  -- ğŸ‰ no goals
#align int.two_pow_two_pow_add_two_pow_two_pow Int.two_pow_two_pow_add_two_pow_two_pow

theorem Int.two_pow_two_pow_sub_pow_two_pow {x y : â„¤} (n : â„•) (hxy : 4 âˆ£ x - y) (hx : Â¬2 âˆ£ x) :
    multiplicity 2 (x ^ 2 ^ n - y ^ 2 ^ n) = multiplicity 2 (x - y) + n := by
  simp only [pow_two_pow_sub_pow_two_pow n, multiplicity.mul Int.prime_two,
    multiplicity.Finset.prod Int.prime_two, add_comm, Nat.cast_one, Finset.sum_const,
    Finset.card_range, nsmul_one, Int.two_pow_two_pow_add_two_pow_two_pow hx hxy]
#align int.two_pow_two_pow_sub_pow_two_pow Int.two_pow_two_pow_sub_pow_two_pow

theorem Int.two_pow_sub_pow' {x y : â„¤} (n : â„•) (hxy : 4 âˆ£ x - y) (hx : Â¬2 âˆ£ x) :
    multiplicity 2 (x ^ n - y ^ n) = multiplicity 2 (x - y) + multiplicity (2 : â„¤) n := by
  have hx_odd : Odd x := by rwa [Int.odd_iff_not_even, even_iff_two_dvd]
  -- âŠ¢ multiplicity 2 (x ^ n - y ^ n) = multiplicity 2 (x - y) + multiplicity 2 â†‘n
  have hxy_even : Even (x - y) := even_iff_two_dvd.mpr (dvd_trans (by norm_num) hxy)
  -- âŠ¢ multiplicity 2 (x ^ n - y ^ n) = multiplicity 2 (x - y) + multiplicity 2 â†‘n
  have hy_odd : Odd y := by simpa using hx_odd.sub_even hxy_even
  -- âŠ¢ multiplicity 2 (x ^ n - y ^ n) = multiplicity 2 (x - y) + multiplicity 2 â†‘n
  cases' n with n
  -- âŠ¢ multiplicity 2 (x ^ Nat.zero - y ^ Nat.zero) = multiplicity 2 (x - y) + mult â€¦
  Â· simp only [pow_zero, sub_self, multiplicity.zero, Int.ofNat_zero, Nat.zero_eq, add_top]
    -- ğŸ‰ no goals
  have h : (multiplicity 2 n.succ).Dom := multiplicity.finite_nat_iff.mpr âŸ¨by norm_num, n.succ_posâŸ©
  -- âŠ¢ multiplicity 2 (x ^ Nat.succ n - y ^ Nat.succ n) = multiplicity 2 (x - y) +  â€¦
  rcases multiplicity.eq_coe_iff.mp (PartENat.natCast_get h).symm with âŸ¨âŸ¨k, hkâŸ©, hpnâŸ©
  -- âŠ¢ multiplicity 2 (x ^ Nat.succ n - y ^ Nat.succ n) = multiplicity 2 (x - y) +  â€¦
  rw [hk, pow_mul, pow_mul, multiplicity.pow_sub_pow_of_prime,
    Int.two_pow_two_pow_sub_pow_two_pow _ hxy hx, â† hk, PartENat.natCast_get]
  Â· norm_cast
    -- ğŸ‰ no goals
  Â· exact Int.prime_two
    -- ğŸ‰ no goals
  Â· simpa only [even_iff_two_dvd] using hx_odd.pow.sub_odd hy_odd.pow
    -- ğŸ‰ no goals
  Â· simpa only [even_iff_two_dvd, Int.odd_iff_not_even] using hx_odd.pow
    -- ğŸ‰ no goals
  erw [Int.coe_nat_dvd]
  -- âŠ¢ Â¬2 âˆ£ k
  -- `erw` to deal with `2 : â„¤` vs `(2 : â„•) : â„¤`
  contrapose! hpn
  -- âŠ¢ 2 ^ (Part.get (multiplicity 2 (Nat.succ n)) h + 1) âˆ£ Nat.succ n
  rw [pow_succ']
  -- âŠ¢ 2 ^ Part.get (multiplicity 2 (Nat.succ n)) h * 2 âˆ£ Nat.succ n
  conv_rhs => rw [hk]
  -- âŠ¢ 2 ^ Part.get (multiplicity 2 (Nat.succ n)) h * 2 âˆ£ 2 ^ Part.get (multiplicit â€¦
  exact mul_dvd_mul_left _ hpn
  -- ğŸ‰ no goals
#align int.two_pow_sub_pow' Int.two_pow_sub_pow'

/-- **Lifting the exponent lemma** for `p = 2` -/
theorem Int.two_pow_sub_pow {x y : â„¤} {n : â„•} (hxy : 2 âˆ£ x - y) (hx : Â¬2 âˆ£ x) (hn : Even n) :
    multiplicity 2 (x ^ n - y ^ n) + 1 =
      multiplicity 2 (x + y) + multiplicity 2 (x - y) + multiplicity (2 : â„¤) n := by
  have hy : Odd y := by
    rw [â† even_iff_two_dvd, â† Int.odd_iff_not_even] at hx
    replace hxy := (@even_neg _ _ (x - y)).mpr (even_iff_two_dvd.mpr hxy)
    convert Even.add_odd hxy hx
    abel
  cases' hn with d hd
  -- âŠ¢ multiplicity 2 (x ^ n - y ^ n) + 1 = multiplicity 2 (x + y) + multiplicity 2 â€¦
  subst hd
  -- âŠ¢ multiplicity 2 (x ^ (d + d) - y ^ (d + d)) + 1 = multiplicity 2 (x + y) + mu â€¦
  simp only [â† two_mul, pow_mul]
  -- âŠ¢ multiplicity 2 ((x ^ 2) ^ d - (y ^ 2) ^ d) + 1 = multiplicity 2 (x + y) + mu â€¦
  have hxy4 : 4 âˆ£ x ^ 2 - y ^ 2 := by
    rw [Int.dvd_iff_emod_eq_zero, Int.sub_emod, Int.sq_mod_four_eq_one_of_odd _,
      Int.sq_mod_four_eq_one_of_odd hy]
    Â· norm_num
    Â· simp only [Int.odd_iff_not_even, even_iff_two_dvd, hx, not_false_iff]
  rw [Int.two_pow_sub_pow' d hxy4 _, sq_sub_sq, â† Int.ofNat_mul_out, multiplicity.mul Int.prime_two,
    multiplicity.mul Int.prime_two]
  suffices multiplicity (2 : â„¤) â†‘(2 : â„•) = 1 by rw [this, add_comm (1 : PartENat), â† add_assoc]
  -- âŠ¢ multiplicity 2 â†‘2 = 1
  Â· norm_cast
    -- âŠ¢ multiplicity 2 2 = 1
    rw [multiplicity.multiplicity_self _ _]
    -- âŠ¢ Â¬IsUnit 2
    Â· apply Prime.not_unit
      -- âŠ¢ Prime 2
      simp only [â† Nat.prime_iff, Nat.prime_two]
      -- ğŸ‰ no goals
    Â· exact two_ne_zero
      -- ğŸ‰ no goals
  Â· rw [â† even_iff_two_dvd, â† Int.odd_iff_not_even]
    -- âŠ¢ Odd (x ^ 2)
    apply Odd.pow
    -- âŠ¢ Odd x
    simp only [Int.odd_iff_not_even, even_iff_two_dvd, hx, not_false_iff]
    -- ğŸ‰ no goals
#align int.two_pow_sub_pow Int.two_pow_sub_pow

theorem Nat.two_pow_sub_pow {x y : â„•} (hxy : 2 âˆ£ x - y) (hx : Â¬2 âˆ£ x) {n : â„•} (hn : Even n) :
    multiplicity 2 (x ^ n - y ^ n) + 1 =
      multiplicity 2 (x + y) + multiplicity 2 (x - y) + multiplicity 2 n := by
  obtain hyx | hyx := le_total y x
  -- âŠ¢ multiplicity 2 (x ^ n - y ^ n) + 1 = multiplicity 2 (x + y) + multiplicity 2 â€¦
  Â· iterate 3 rw [â† multiplicity.Int.coe_nat_multiplicity]
    -- âŠ¢ multiplicity â†‘2 â†‘(x ^ n - y ^ n) + 1 = multiplicity â†‘2 â†‘(x + y) + multiplici â€¦
    simp only [Int.ofNat_sub hyx, Int.ofNat_sub (pow_le_pow_of_le_left' hyx _), Int.ofNat_add,
      Int.coe_nat_pow]
    rw [â† Int.coe_nat_dvd] at hx
    -- âŠ¢ multiplicity (â†‘2) (â†‘x ^ n - â†‘y ^ n) + 1 = multiplicity (â†‘2) (â†‘x + â†‘y) + mult â€¦
    rw [â† Int.coe_nat_dvd, Int.ofNat_sub hyx] at hxy
    -- âŠ¢ multiplicity (â†‘2) (â†‘x ^ n - â†‘y ^ n) + 1 = multiplicity (â†‘2) (â†‘x + â†‘y) + mult â€¦
    convert Int.two_pow_sub_pow hxy hx hn using 2
    -- âŠ¢ multiplicity 2 n = multiplicity 2 â†‘n
    rw [â† multiplicity.Int.coe_nat_multiplicity]
    -- âŠ¢ multiplicity â†‘2 â†‘n = multiplicity 2 â†‘n
    rfl
    -- ğŸ‰ no goals
  Â· simp only [Nat.sub_eq_zero_iff_le.mpr hyx,
      Nat.sub_eq_zero_iff_le.mpr (pow_le_pow_of_le_left' hyx n), multiplicity.zero,
      PartENat.top_add, PartENat.add_top]
#align nat.two_pow_sub_pow Nat.two_pow_sub_pow

namespace padicValNat

variable {x y : â„•}

theorem pow_two_sub_pow (hyx : y < x) (hxy : 2 âˆ£ x - y) (hx : Â¬2 âˆ£ x) {n : â„•} (hn : 0 < n)
    (hneven : Even n) :
    padicValNat 2 (x ^ n - y ^ n) + 1 =
      padicValNat 2 (x + y) + padicValNat 2 (x - y) + padicValNat 2 n := by
  simp only [â† PartENat.natCast_inj, Nat.cast_add]
  -- âŠ¢ â†‘(padicValNat 2 (x ^ n - y ^ n)) + â†‘1 = â†‘(padicValNat 2 (x + y)) + â†‘(padicVa â€¦
  iterate 4 rw [padicValNat_def, PartENat.natCast_get]
  Â· convert Nat.two_pow_sub_pow hxy hx hneven using 2
    -- ğŸ‰ no goals
  Â· exact hn
    -- ğŸ‰ no goals
  Â· exact Nat.sub_pos_of_lt hyx
    -- ğŸ‰ no goals
  Â· linarith
    -- ğŸ‰ no goals
  Â· simp only [tsub_pos_iff_lt, pow_lt_pow_of_lt_left hyx (@zero_le' _ y _) hn]
    -- ğŸ‰ no goals
#align padic_val_nat.pow_two_sub_pow padicValNat.pow_two_sub_pow

variable {p : â„•} [hp : Fact p.Prime] (hp1 : Odd p)

theorem pow_sub_pow (hyx : y < x) (hxy : p âˆ£ x - y) (hx : Â¬p âˆ£ x) {n : â„•} (hn : 0 < n) :
    padicValNat p (x ^ n - y ^ n) = padicValNat p (x - y) + padicValNat p n := by
  rw [â† PartENat.natCast_inj, Nat.cast_add]
  -- âŠ¢ â†‘(padicValNat p (x ^ n - y ^ n)) = â†‘(padicValNat p (x - y)) + â†‘(padicValNat  â€¦
  iterate 3 rw [padicValNat_def, PartENat.natCast_get]
  Â· exact multiplicity.Nat.pow_sub_pow hp.out hp1 hxy hx n
    -- ğŸ‰ no goals
  Â· exact hn
    -- ğŸ‰ no goals
  Â· exact Nat.sub_pos_of_lt hyx
    -- ğŸ‰ no goals
  Â· exact Nat.sub_pos_of_lt (Nat.pow_lt_pow_of_lt_left hyx hn)
    -- ğŸ‰ no goals
#align padic_val_nat.pow_sub_pow padicValNat.pow_sub_pow

theorem pow_add_pow (hxy : p âˆ£ x + y) (hx : Â¬p âˆ£ x) {n : â„•} (hn : Odd n) :
    padicValNat p (x ^ n + y ^ n) = padicValNat p (x + y) + padicValNat p n := by
  cases' y with y
  -- âŠ¢ padicValNat p (x ^ n + Nat.zero ^ n) = padicValNat p (x + Nat.zero) + padicV â€¦
  Â· contradiction
    -- ğŸ‰ no goals
  rw [â† PartENat.natCast_inj, Nat.cast_add]
  -- âŠ¢ â†‘(padicValNat p (x ^ n + Nat.succ y ^ n)) = â†‘(padicValNat p (x + Nat.succ y) â€¦
  iterate 3 rw [padicValNat_def, PartENat.natCast_get]
  Â· exact multiplicity.Nat.pow_add_pow hp.out hp1 hxy hx hn
    -- ğŸ‰ no goals
  Â· exact Odd.pos hn
    -- ğŸ‰ no goals
  Â· simp only [add_pos_iff, Nat.succ_pos', or_true_iff]
    -- ğŸ‰ no goals
  Â· exact Nat.lt_add_left _ _ _ (pow_pos y.succ_pos _)
    -- ğŸ‰ no goals
#align padic_val_nat.pow_add_pow padicValNat.pow_add_pow

end padicValNat
