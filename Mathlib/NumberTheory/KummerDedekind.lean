/-
Copyright (c) 2021 Anne Baanen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anne Baanen, Paul Lezeau
-/
import Mathlib.RingTheory.DedekindDomain.Ideal
import Mathlib.RingTheory.IsAdjoinRoot

#align_import number_theory.kummer_dedekind from "leanprover-community/mathlib"@"f0c8bf9245297a541f468be517f1bde6195105e9"

/-!
# Kummer-Dedekind theorem

This file proves the monogenic version of the Kummer-Dedekind theorem on the splitting of prime
ideals in an extension of the ring of integers. This states that if `I` is a prime ideal of
Dedekind domain `R` and `S = R[Î±]` for some `Î±` that is integral over `R` with minimal polynomial
`f`, then the prime factorisations of `I * S` and `f mod I` have the same shape, i.e. they have the
same number of prime factors, and each prime factors of `I * S` can be paired with a prime factor
of `f mod I` in a way that ensures multiplicities match (in fact, this pairing can be made explicit
with a formula).

## Main definitions

 * `normalizedFactorsMapEquivNormalizedFactorsMinPolyMk` : The bijection in the
    Kummer-Dedekind theorem. This is the pairing between the prime factors of `I * S` and the prime
    factors of `f mod I`.

## Main results

 * `normalized_factors_ideal_map_eq_normalized_factors_min_poly_mk_map` : The Kummer-Dedekind
    theorem.
 * `Ideal.irreducible_map_of_irreducible_minpoly` : `I.map (algebraMap R S)` is irreducible if
    `(map (Ideal.Quotient.mk I) (minpoly R pb.gen))` is irreducible, where `pb` is a power basis
    of `S` over `R`.

## TODO

 * Prove the Kummer-Dedekind theorem in full generality.

 * Prove the converse of `Ideal.irreducible_map_of_irreducible_minpoly`.

 * Prove that `normalizedFactorsMapEquivNormalizedFactorsMinPolyMk` can be expressed as
    `normalizedFactorsMapEquivNormalizedFactorsMinPolyMk g = âŸ¨I, G(Î±)âŸ©` for `g` a prime
    factor of `f mod I` and `G` a lift of `g` to `R[X]`.

## References

 * [J. Neukirch, *Algebraic Number Theory*][Neukirch1992]

## Tags

kummer, dedekind, kummer dedekind, dedekind-kummer, dedekind kummer
-/


variable (R : Type*) {S : Type*} [CommRing R] [CommRing S] [Algebra R S]

open Ideal Polynomial DoubleQuot UniqueFactorizationMonoid Algebra RingHom

local notation:max R "<" x:max ">" => adjoin R ({x} : Set S)

/-- Let `S / R` be a ring extension and `x : S`, then the conductor of `R<x>` is the
    biggest ideal of `S` contained in `R<x>`. -/
def conductor (x : S) : Ideal S where
  carrier := {a | âˆ€ b : S, a * b âˆˆ R<x>}
  zero_mem' b := by simpa only [zero_mul] using Subalgebra.zero_mem _
                    -- ğŸ‰ no goals
                         -- ğŸ‰ no goals
  add_mem' ha hb c := by simpa only [add_mul] using Subalgebra.add_mem _ (ha c) (hb c)
  smul_mem' c a ha b := by simpa only [smul_eq_mul, mul_left_comm, mul_assoc] using ha (c * b)
                           -- ğŸ‰ no goals
#align conductor conductor

variable {R} {x : S}

theorem conductor_eq_of_eq {y : S} (h : (R<x> : Set S) = R<y>) : conductor R x = conductor R y :=
  Ideal.ext fun _ => forall_congr' fun _ => Set.ext_iff.mp h _
#align conductor_eq_of_eq conductor_eq_of_eq

theorem conductor_subset_adjoin : (conductor R x : Set S) âŠ† R<x> := fun y hy => by
  simpa only [mul_one] using hy 1
  -- ğŸ‰ no goals
#align conductor_subset_adjoin conductor_subset_adjoin

theorem mem_conductor_iff {y : S} : y âˆˆ conductor R x â†” âˆ€ b : S, y * b âˆˆ R<x> :=
  âŸ¨fun h => h, fun h => hâŸ©
#align mem_conductor_iff mem_conductor_iff

theorem conductor_eq_top_of_adjoin_eq_top (h : R<x> = âŠ¤) : conductor R x = âŠ¤ := by
  simp only [Ideal.eq_top_iff_one, mem_conductor_iff, h, mem_top, forall_const]
  -- ğŸ‰ no goals
#align conductor_eq_top_of_adjoin_eq_top conductor_eq_top_of_adjoin_eq_top

theorem conductor_eq_top_of_powerBasis (pb : PowerBasis R S) : conductor R pb.gen = âŠ¤ :=
  conductor_eq_top_of_adjoin_eq_top pb.adjoin_gen_eq_top
#align conductor_eq_top_of_power_basis conductor_eq_top_of_powerBasis

variable {I : Ideal R}

/-- This technical lemma tell us that if `C` is the conductor of `R<x>` and `I` is an ideal of `R`
  then `p * (I * S) âŠ† I * R<x>` for any `p` in `C âˆ© R` -/
theorem prod_mem_ideal_map_of_mem_conductor {p : R} {z : S}
    (hp : p âˆˆ Ideal.comap (algebraMap R S) (conductor R x)) (hz' : z âˆˆ I.map (algebraMap R S)) :
    algebraMap R S p * z âˆˆ algebraMap R<x> S '' â†‘(I.map (algebraMap R R<x>)) := by
  rw [Ideal.map, Ideal.span, Finsupp.mem_span_image_iff_total] at hz'
  -- âŠ¢ â†‘(algebraMap R S) p * z âˆˆ â†‘(algebraMap { x_1 // x_1 âˆˆ adjoin R {x} } S) '' â†‘ â€¦
  obtain âŸ¨l, H, H'âŸ© := hz'
  -- âŠ¢ â†‘(algebraMap R S) p * z âˆˆ â†‘(algebraMap { x_1 // x_1 âˆˆ adjoin R {x} } S) '' â†‘ â€¦
  rw [Finsupp.total_apply] at H'
  -- âŠ¢ â†‘(algebraMap R S) p * z âˆˆ â†‘(algebraMap { x_1 // x_1 âˆˆ adjoin R {x} } S) '' â†‘ â€¦
  rw [â† H', mul_comm, Finsupp.sum_mul]
  -- âŠ¢ (Finsupp.sum l fun a c => c â€¢ â†‘(algebraMap R S) a * â†‘(algebraMap R S) p) âˆˆ â†‘ â€¦
  have lem : âˆ€ {a : R}, a âˆˆ I â†’ l a â€¢ algebraMap R S a * algebraMap R S p âˆˆ
      algebraMap R<x> S '' I.map (algebraMap R R<x>) := by
    intro a ha
    rw [Algebra.id.smul_eq_mul, mul_assoc, mul_comm, mul_assoc, Set.mem_image]
    refine Exists.intro
        (algebraMap R R<x> a * âŸ¨l a * algebraMap R S p,
          show l a * algebraMap R S p âˆˆ R<x> from ?_âŸ©) ?_
    Â· refine âŸ¨?_, ?_âŸ©
      Â· rw [mul_comm]
        exact mem_conductor_iff.mp (Ideal.mem_comap.mp hp) _
      Â· rw [mul_comm]
        apply Ideal.mul_mem_left (I.map (algebraMap R R<x>)) _ (Ideal.mem_map_of_mem _ ha)
      Â· simp only [RingHom.map_mul, mul_comm (algebraMap R S p) (l a)]
        rfl
  refine Finset.sum_induction _ (fun u => u âˆˆ algebraMap R<x> S '' I.map (algebraMap R R<x>))
      (fun a b => ?_) ?_ ?_
  rintro âŸ¨z, hz, rflâŸ© âŸ¨y, hy, rflâŸ©
  rw [â† RingHom.map_add]
  exact âŸ¨z + y, Ideal.add_mem _ (SetLike.mem_coe.mp hz) hy, rflâŸ©
  -- âŠ¢ (fun u => u âˆˆ â†‘(algebraMap { x_1 // x_1 âˆˆ adjoin R {x} } S) '' â†‘(Ideal.map ( â€¦
  Â· exact âŸ¨0, SetLike.mem_coe.mpr <| Ideal.zero_mem _, RingHom.map_zero _âŸ©
    -- ğŸ‰ no goals
  Â· intro y hy
    -- âŠ¢ (fun a c => c â€¢ â†‘(algebraMap R S) a * â†‘(algebraMap R S) p) y (â†‘l y) âˆˆ â†‘(alge â€¦
    exact lem ((Finsupp.mem_supported _ l).mp H hy)
    -- ğŸ‰ no goals
#align prod_mem_ideal_map_of_mem_conductor prod_mem_ideal_map_of_mem_conductor

/-- A technical result telling us that `(I * S) âˆ© R<x> = I * R<x>` for any ideal `I` of `R`. -/
theorem comap_map_eq_map_adjoin_of_coprime_conductor
    (hx : (conductor R x).comap (algebraMap R S) âŠ” I = âŠ¤)
    (h_alg : Function.Injective (algebraMap R<x> S)) :
    (I.map (algebraMap R S)).comap (algebraMap R<x> S) = I.map (algebraMap R R<x>) := by
  apply le_antisymm
  -- âŠ¢ comap (algebraMap { x_1 // x_1 âˆˆ adjoin R {x} } S) (Ideal.map (algebraMap R  â€¦
  Â· -- This is adapted from [Neukirch1992]. Let `C = (conductor R x)`. The idea of the proof
    -- is that since `I` and `C âˆ© R` are coprime, we have
    -- `(I * S) âˆ© R<x> âŠ† (I + C) * ((I * S) âˆ© R<x>) âŠ† I * R<x> + I * C * S âŠ† I * R<x>`.
    intro y hy
    -- âŠ¢ y âˆˆ Ideal.map (algebraMap R { x_1 // x_1 âˆˆ adjoin R {x} }) I
    obtain âŸ¨z, hzâŸ© := y
    -- âŠ¢ { val := z, property := hz } âˆˆ Ideal.map (algebraMap R { x_1 // x_1 âˆˆ adjoin â€¦
    obtain âŸ¨p, hp, q, hq, hpqâŸ© := Submodule.mem_sup.mp ((Ideal.eq_top_iff_one _).mp hx)
    -- âŠ¢ { val := z, property := hz } âˆˆ Ideal.map (algebraMap R { x_1 // x_1 âˆˆ adjoin â€¦
    have temp : algebraMap R S p * z + algebraMap R S q * z = z := by
      simp only [â† add_mul, â† RingHom.map_add (algebraMap R S), hpq, map_one, one_mul]
    suffices z âˆˆ algebraMap R<x> S '' I.map (algebraMap R R<x>) â†”
        (âŸ¨z, hzâŸ© : R<x>) âˆˆ I.map (algebraMap R R<x>) by
      rw [â† this, â† temp]
      obtain âŸ¨a, haâŸ© := (Set.mem_image _ _ _).mp (prod_mem_ideal_map_of_mem_conductor hp
          (show z âˆˆ I.map (algebraMap R S) by rwa [Ideal.mem_comap] at hy ))
      use a + algebraMap R R<x> q * âŸ¨z, hzâŸ©
      refine âŸ¨Ideal.add_mem (I.map (algebraMap R R<x>)) ha.left ?_, by
          simp only [ha.right, map_add, AlgHom.map_mul, add_right_inj]; rflâŸ©
      rw [mul_comm]
      exact Ideal.mul_mem_left (I.map (algebraMap R R<x>)) _ (Ideal.mem_map_of_mem _ hq)
    refine âŸ¨fun h => ?_,
      fun h => (Set.mem_image _ _ _).mpr (Exists.intro âŸ¨z, hzâŸ© âŸ¨by simp [h], rflâŸ©)âŸ©
    Â· obtain âŸ¨xâ‚, hxâ‚, hxâ‚‚âŸ© := (Set.mem_image _ _ _).mp h
      -- âŠ¢ { val := z, property := hz } âˆˆ Ideal.map (algebraMap R { x_1 // x_1 âˆˆ adjoin â€¦
      have : xâ‚ = âŸ¨z, hzâŸ© := by
        apply h_alg
        simp [hxâ‚‚]
        rfl
      rwa [â† this]
      -- ğŸ‰ no goals
  Â· -- The converse inclusion is trivial
    have : algebraMap R S = (algebraMap _ S).comp (algebraMap R R<x>) := by ext; rfl
    -- âŠ¢ Ideal.map (algebraMap R { x_1 // x_1 âˆˆ adjoin R {x} }) I â‰¤ comap (algebraMap â€¦
    rw [this, â† Ideal.map_map]
    -- âŠ¢ Ideal.map (algebraMap R { x_1 // x_1 âˆˆ adjoin R {x} }) I â‰¤ comap (algebraMap â€¦
    apply Ideal.le_comap_map
    -- ğŸ‰ no goals
#align comap_map_eq_map_adjoin_of_coprime_conductor comap_map_eq_map_adjoin_of_coprime_conductor

/-- The canonical morphism of rings from `R<x> â§¸ (I*R<x>)` to `S â§¸ (I*S)` is an isomorphism
    when `I` and `(conductor R x) âˆ© R` are coprime. -/
noncomputable def quotAdjoinEquivQuotMap (hx : (conductor R x).comap (algebraMap R S) âŠ” I = âŠ¤)
    (h_alg : Function.Injective (algebraMap R<x> S)) :
    R<x> â§¸ I.map (algebraMap R R<x>) â‰ƒ+* S â§¸ I.map (algebraMap R S) := by
  let f : R<x> â§¸ I.map (algebraMap R R<x>) â†’+* S â§¸ I.map (algebraMap R S) :=
    (Ideal.Quotient.lift (I.map (algebraMap R R<x>))
      ((Ideal.Quotient.mk (I.map (algebraMap R S))).comp (algebraMap R<x> S)) (fun r hr => by
      have : algebraMap R S = (algebraMap R<x> S).comp (algebraMap R R<x>) := by ext; rfl
      rw [RingHom.comp_apply, Ideal.Quotient.eq_zero_iff_mem, this, â† Ideal.map_map]
      exact Ideal.mem_map_of_mem _ hr))
  refine RingEquiv.ofBijective f âŸ¨?_, ?_âŸ©
  -- âŠ¢ Function.Injective â†‘f
  Â· --the kernel of the map is clearly `(I * S) âˆ© R<x>`. To get injectivity, we need to show that
    --this is contained in `I * R<x>`, which is the content of the previous lemma.
    refine RingHom.lift_injective_of_ker_le_ideal _ _ fun u hu => ?_
    -- âŠ¢ u âˆˆ Ideal.map (algebraMap R { x_1 // x_1 âˆˆ adjoin R {x} }) I
    rwa [RingHom.mem_ker, RingHom.comp_apply, Ideal.Quotient.eq_zero_iff_mem, â† Ideal.mem_comap,
      comap_map_eq_map_adjoin_of_coprime_conductor hx h_alg] at hu
  Â· -- Surjectivity follows from the surjectivity of the canonical map `R<x> â†’ S â§¸ (I * S)`,
    -- which in turn follows from the fact that `I * S + (conductor R x) = S`.
    refine Ideal.Quotient.lift_surjective_of_surjective _ _ fun y => ?_
    -- âŠ¢ âˆƒ a, â†‘(RingHom.comp (Ideal.Quotient.mk (Ideal.map (algebraMap R S) I)) (alge â€¦
    obtain âŸ¨z, hzâŸ© := Ideal.Quotient.mk_surjective y
    -- âŠ¢ âˆƒ a, â†‘(RingHom.comp (Ideal.Quotient.mk (Ideal.map (algebraMap R S) I)) (alge â€¦
    have : z âˆˆ conductor R x âŠ” I.map (algebraMap R S) := by
      suffices conductor R x âŠ” I.map (algebraMap R S) = âŠ¤ by simp only [this, Submodule.mem_top]
      rw [Ideal.eq_top_iff_one] at hx âŠ¢
      replace hx := Ideal.mem_map_of_mem (algebraMap R S) hx
      rw [Ideal.map_sup, RingHom.map_one] at hx
      exact (sup_le_sup
        (show ((conductor R x).comap (algebraMap R S)).map (algebraMap R S) â‰¤ conductor R x
          from Ideal.map_comap_le)
          (le_refl (I.map (algebraMap R S)))) hx
    rw [â† Ideal.mem_quotient_iff_mem_sup, hz, Ideal.mem_map_iff_of_surjective] at this
    -- âŠ¢ âˆƒ a, â†‘(RingHom.comp (Ideal.Quotient.mk (Ideal.map (algebraMap R S) I)) (alge â€¦
    obtain âŸ¨u, hu, hu'âŸ© := this
    -- âŠ¢ âˆƒ a, â†‘(RingHom.comp (Ideal.Quotient.mk (Ideal.map (algebraMap R S) I)) (alge â€¦
    use âŸ¨u, conductor_subset_adjoin huâŸ©
    -- âŠ¢ â†‘(RingHom.comp (Ideal.Quotient.mk (Ideal.map (algebraMap R S) I)) (algebraMa â€¦
    simp only [â† hu']
    -- âŠ¢ â†‘(RingHom.comp (Ideal.Quotient.mk (Ideal.map (algebraMap R S) I)) (algebraMa â€¦
    rfl
    -- âŠ¢ Function.Surjective â†‘(Ideal.Quotient.mk (Ideal.map (algebraMap R S) I))
    Â· exact Ideal.Quotient.mk_surjective
      -- ğŸ‰ no goals
#align quot_adjoin_equiv_quot_map quotAdjoinEquivQuotMap

-- Porting note: on-line linter fails with `failed to synthesize` instance
-- but #lint does not report any problem
@[simp, nolint simpNF]
theorem quotAdjoinEquivQuotMap_apply_mk (hx : (conductor R x).comap (algebraMap R S) âŠ” I = âŠ¤)
    (h_alg : Function.Injective (algebraMap R<x> S)) (a : R<x>) :
    quotAdjoinEquivQuotMap hx h_alg (Ideal.Quotient.mk (I.map (algebraMap R R<x>)) a) =
      Ideal.Quotient.mk (I.map (algebraMap R S)) â†‘a := rfl
#align quot_adjoin_equiv_quot_map_apply_mk quotAdjoinEquivQuotMap_apply_mk

namespace KummerDedekind

open scoped BigOperators Polynomial Classical

variable [IsDomain R] [IsIntegrallyClosed R]

variable [IsDomain S] [IsDedekindDomain S]

variable [NoZeroSMulDivisors R S]

attribute [local instance] Ideal.Quotient.field

/-- The first half of the **Kummer-Dedekind Theorem** in the monogenic case, stating that the prime
    factors of `I*S` are in bijection with those of the minimal polynomial of the generator of `S`
    over `R`, taken `mod I`.-/
noncomputable def normalizedFactorsMapEquivNormalizedFactorsMinPolyMk (hI : IsMaximal I)
    (hI' : I â‰  âŠ¥) (hx : (conductor R x).comap (algebraMap R S) âŠ” I = âŠ¤) (hx' : IsIntegral R x) :
    {J : Ideal S | J âˆˆ normalizedFactors (I.map (algebraMap R S))} â‰ƒ
      {d : (R â§¸ I)[X] |
        d âˆˆ normalizedFactors (Polynomial.map (Ideal.Quotient.mk I) (minpoly R x))} := by
  -- Porting note: Lean needs to be reminded about this so it does not time out
  have : IsPrincipalIdealRing (R â§¸ I)[X] := inferInstance
  -- âŠ¢ â†‘{J | J âˆˆ normalizedFactors (Ideal.map (algebraMap R S) I)} â‰ƒ â†‘{d | d âˆˆ norm â€¦
  let f : S â§¸ map (algebraMap R S) I â‰ƒ+*
    (R â§¸ I)[X] â§¸ span {Polynomial.map (Ideal.Quotient.mk I) (minpoly R x)} := by
    refine (quotAdjoinEquivQuotMap hx ?_).symm.trans
      (((Algebra.adjoin.powerBasis'
        hx').quotientEquivQuotientMinpolyMap I).toRingEquiv.trans (quotEquivOfEq ?_))
    Â· exact NoZeroSMulDivisors.algebraMap_injective (Algebra.adjoin R {x}) S
    Â· rw [Algebra.adjoin.powerBasis'_minpoly_gen hx']
  refine (normalizedFactorsEquivOfQuotEquiv f ?_ ?_).trans ?_
  Â· rwa [Ne.def, map_eq_bot_iff_of_injective (NoZeroSMulDivisors.algebraMap_injective R S),
      â† Ne.def]
  Â· by_contra h
    -- âŠ¢ False
    exact (show Polynomial.map (Ideal.Quotient.mk I) (minpoly R x) â‰  0 from
      Polynomial.map_monic_ne_zero (minpoly.monic hx')) (span_singleton_eq_bot.mp h)
  Â· refine (normalizedFactorsEquivSpanNormalizedFactors ?_).symm
    -- âŠ¢ Polynomial.map (Ideal.Quotient.mk I) (minpoly R x) â‰  0
    exact Polynomial.map_monic_ne_zero (minpoly.monic hx')
    -- ğŸ‰ no goals
#align kummer_dedekind.normalized_factors_map_equiv_normalized_factors_min_poly_mk KummerDedekind.normalizedFactorsMapEquivNormalizedFactorsMinPolyMk

/-- The second half of the **Kummer-Dedekind Theorem** in the monogenic case, stating that the
    bijection `FactorsEquiv'` defined in the first half preserves multiplicities. -/
theorem multiplicity_factors_map_eq_multiplicity (hI : IsMaximal I) (hI' : I â‰  âŠ¥)
    (hx : (conductor R x).comap (algebraMap R S) âŠ” I = âŠ¤) (hx' : IsIntegral R x) {J : Ideal S}
    (hJ : J âˆˆ normalizedFactors (I.map (algebraMap R S))) :
    multiplicity J (I.map (algebraMap R S)) =
      multiplicity (â†‘(normalizedFactorsMapEquivNormalizedFactorsMinPolyMk hI hI' hx hx' âŸ¨J, hJâŸ©))
        (Polynomial.map (Ideal.Quotient.mk I) (minpoly R x)) := by
  rw [normalizedFactorsMapEquivNormalizedFactorsMinPolyMk, Equiv.coe_trans, Function.comp_apply,
    multiplicity_normalizedFactorsEquivSpanNormalizedFactors_symm_eq_multiplicity,
    normalizedFactorsEquivOfQuotEquiv_multiplicity_eq_multiplicity]
#align kummer_dedekind.multiplicity_factors_map_eq_multiplicity KummerDedekind.multiplicity_factors_map_eq_multiplicity

/-- The **Kummer-Dedekind Theorem**. -/
theorem normalizedFactors_ideal_map_eq_normalizedFactors_min_poly_mk_map (hI : IsMaximal I)
    (hI' : I â‰  âŠ¥) (hx : (conductor R x).comap (algebraMap R S) âŠ” I = âŠ¤) (hx' : IsIntegral R x) :
    normalizedFactors (I.map (algebraMap R S)) =
      Multiset.map
        (fun f =>
          ((normalizedFactorsMapEquivNormalizedFactorsMinPolyMk hI hI' hx hx').symm f : Ideal S))
        (normalizedFactors (Polynomial.map (Ideal.Quotient.mk I) (minpoly R x))).attach := by
  ext J
  -- âŠ¢ Multiset.count J (normalizedFactors (Ideal.map (algebraMap R S) I)) = Multis â€¦
  -- WLOG, assume J is a normalized factor
  by_cases hJ : J âˆˆ normalizedFactors (I.map (algebraMap R S));
  -- âŠ¢ Multiset.count J (normalizedFactors (Ideal.map (algebraMap R S) I)) = Multis â€¦
  swap
  -- âŠ¢ Multiset.count J (normalizedFactors (Ideal.map (algebraMap R S) I)) = Multis â€¦
  Â· rw [Multiset.count_eq_zero.mpr hJ, eq_comm, Multiset.count_eq_zero, Multiset.mem_map]
    -- âŠ¢ Â¬âˆƒ a, a âˆˆ Multiset.attach (normalizedFactors (Polynomial.map (Ideal.Quotient â€¦
    simp only [Multiset.mem_attach, true_and_iff, not_exists]
    -- âŠ¢ âˆ€ (x_1 : â†‘{d | d âˆˆ normalizedFactors (Polynomial.map (Ideal.Quotient.mk I) ( â€¦
    rintro J' rfl
    -- âŠ¢ False
    exact
      hJ ((normalizedFactorsMapEquivNormalizedFactorsMinPolyMk hI hI' hx hx').symm J').prop
  -- Then we just have to compare the multiplicities, which we already proved are equal.
  have := multiplicity_factors_map_eq_multiplicity hI hI' hx hx' hJ
  -- âŠ¢ Multiset.count J (normalizedFactors (Ideal.map (algebraMap R S) I)) = Multis â€¦
  rw [multiplicity_eq_count_normalizedFactors, multiplicity_eq_count_normalizedFactors,
    UniqueFactorizationMonoid.normalize_normalized_factor _ hJ,
    UniqueFactorizationMonoid.normalize_normalized_factor, PartENat.natCast_inj] at this
  refine' this.trans _
  -- Get rid of the `map` by applying the equiv to both sides.
  generalize hJ' :
    (normalizedFactorsMapEquivNormalizedFactorsMinPolyMk hI hI' hx hx') âŸ¨J, hJâŸ© = J'
  have : ((normalizedFactorsMapEquivNormalizedFactorsMinPolyMk hI hI' hx hx').symm J' : Ideal S) =
      J :=
    by rw [â† hJ', Equiv.symm_apply_apply _ _, Subtype.coe_mk]
  subst this
  -- Get rid of the `attach` by applying the subtype `coe` to both sides.
  rw [Multiset.count_map_eq_count' fun f =>
      ((normalizedFactorsMapEquivNormalizedFactorsMinPolyMk hI hI' hx hx').symm f :
        Ideal S),
    Multiset.attach_count_eq_count_coe]
  Â· exact Subtype.coe_injective.comp (Equiv.injective _)
    -- ğŸ‰ no goals
  Â· exact (normalizedFactorsMapEquivNormalizedFactorsMinPolyMk hI hI' hx hx' _).prop
    -- ğŸ‰ no goals
  Â· exact irreducible_of_normalized_factor _
        (normalizedFactorsMapEquivNormalizedFactorsMinPolyMk hI hI' hx hx' _).prop
  Â· exact Polynomial.map_monic_ne_zero (minpoly.monic hx')
    -- ğŸ‰ no goals
  Â· exact irreducible_of_normalized_factor _ hJ
    -- ğŸ‰ no goals
  Â· rwa [â† bot_eq_zero, Ne.def,
      map_eq_bot_iff_of_injective (NoZeroSMulDivisors.algebraMap_injective R S)]
#align kummer_dedekind.normalized_factors_ideal_map_eq_normalized_factors_min_poly_mk_map KummerDedekind.normalizedFactors_ideal_map_eq_normalizedFactors_min_poly_mk_map

theorem Ideal.irreducible_map_of_irreducible_minpoly (hI : IsMaximal I) (hI' : I â‰  âŠ¥)
    (hx : (conductor R x).comap (algebraMap R S) âŠ” I = âŠ¤) (hx' : IsIntegral R x)
    (hf : Irreducible (Polynomial.map (Ideal.Quotient.mk I) (minpoly R x))) :
    Irreducible (I.map (algebraMap R S)) := by
  have mem_norm_factors : normalize (Polynomial.map (Ideal.Quotient.mk I) (minpoly R x)) âˆˆ
      normalizedFactors (Polynomial.map (Ideal.Quotient.mk I) (minpoly R x)) :=
    by simp [normalizedFactors_irreducible hf]
  suffices âˆƒ y, normalizedFactors (I.map (algebraMap R S)) = {y} by
    obtain âŸ¨y, hyâŸ© := this
    have h := normalizedFactors_prod (show I.map (algebraMap R S) â‰  0 by
          rwa [â† bot_eq_zero, Ne.def,
            map_eq_bot_iff_of_injective (NoZeroSMulDivisors.algebraMap_injective R S)])
    rw [associated_iff_eq, hy, Multiset.prod_singleton] at h
    rw [â† h]
    exact
      irreducible_of_normalized_factor y
        (show y âˆˆ normalizedFactors (I.map (algebraMap R S)) by simp [hy])
  rw [normalizedFactors_ideal_map_eq_normalizedFactors_min_poly_mk_map hI hI' hx hx']
  -- âŠ¢ âˆƒ y, Multiset.map (fun f => â†‘(â†‘(normalizedFactorsMapEquivNormalizedFactorsMi â€¦
  use ((normalizedFactorsMapEquivNormalizedFactorsMinPolyMk hI hI' hx hx').symm
        âŸ¨normalize (Polynomial.map (Ideal.Quotient.mk I) (minpoly R x)), mem_norm_factorsâŸ© :
      Ideal S)
  rw [Multiset.map_eq_singleton]
  -- âŠ¢ âˆƒ a, Multiset.attach (normalizedFactors (Polynomial.map (Ideal.Quotient.mk I â€¦
  use âŸ¨normalize (Polynomial.map (Ideal.Quotient.mk I) (minpoly R x)), mem_norm_factorsâŸ©
  -- âŠ¢ Multiset.attach (normalizedFactors (Polynomial.map (Ideal.Quotient.mk I) (mi â€¦
  refine âŸ¨?_, rflâŸ©
  -- âŠ¢ Multiset.attach (normalizedFactors (Polynomial.map (Ideal.Quotient.mk I) (mi â€¦
  apply Multiset.map_injective Subtype.coe_injective
  -- âŠ¢ Multiset.map (fun a => â†‘a) (Multiset.attach (normalizedFactors (Polynomial.m â€¦
  rw [Multiset.attach_map_val, Multiset.map_singleton, Subtype.coe_mk]
  -- âŠ¢ normalizedFactors (Polynomial.map (Ideal.Quotient.mk I) (minpoly R x)) = {â†‘n â€¦
  exact normalizedFactors_irreducible hf
  -- ğŸ‰ no goals
#align kummer_dedekind.ideal.irreducible_map_of_irreducible_minpoly KummerDedekind.Ideal.irreducible_map_of_irreducible_minpoly

end KummerDedekind
