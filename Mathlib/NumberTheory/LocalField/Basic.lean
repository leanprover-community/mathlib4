/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.RingTheory.Valuation.DiscreteValuativeRel
import Mathlib.Topology.Algebra.Valued.LocallyCompact
import Mathlib.Topology.Algebra.Valued.ValuativeRel

/-!

# Definition of (Non-archimedean) local fields

Given a topological field `K` equipped with an equivalence class of valuations (a `ValuativeRel`),
we say that it is a non-archimedean local field if the topology comes from the given valuation,
and it is locally compact and non-discrete.

-/

/--
Given a topological field `K` equipped with an equivalence class of valuations (a `ValuativeRel`),
we say that it is a non-archimedean local field if the topology comes from the given valuation,
and it is locally compact and non-discrete.

This implies the following typeclasses via `inferInstance`
- `IsValuativeTopology K`
- `LocallyCompactSpace K`
- `IsTopologicalDivisionRing K`
- `ValuativeRel.IsNontrivial K`
- `ValuativeRel.IsRankLeOne K`
- `ValuativeRel.IsDiscrete K`
- `IsDiscreteValuationRing ùí™[K]`
- `Finite ùìÄ[K]`

Assuming we have a compatible `UniformSpace K` instance
(e.g. via `IsTopologicalAddGroup.toUniformSpace` and `isUniformAddGroup_of_addCommGroup`) then
- `CompleteSpace K`
- `CompleteSpace ùí™[K]`
-/
class IsNonarchimedeanLocalField
    (K : Type*) [Field K] [ValuativeRel K] [TopologicalSpace K] : Prop extends
  IsValuativeTopology K,
  LocallyCompactSpace K,
  ValuativeRel.IsNontrivial K

open ValuativeRel Valued.integer

open scoped WithZero

namespace IsNonarchimedeanLocalField

section TopologicalSpace

variable (K : Type*) [Field K] [ValuativeRel K] [TopologicalSpace K] [IsNonarchimedeanLocalField K]

attribute [local simp] zero_lt_iff

instance : IsTopologicalDivisionRing K := by
  letI := IsTopologicalAddGroup.toUniformSpace K
  haveI := isUniformAddGroup_of_addCommGroup (G := K)
  infer_instance

lemma isCompact_closedBall (Œ≥ : ValueGroupWithZero K) : IsCompact { x | valuation K x ‚â§ Œ≥ } := by
  obtain ‚ü®Œ≥, rfl‚ü© := ValuativeRel.valuation_surjective Œ≥
  by_cases hŒ≥ : Œ≥ = 0
  ¬∑ simp [hŒ≥]
  letI := IsTopologicalAddGroup.toUniformSpace K
  letI := isUniformAddGroup_of_addCommGroup (G := K)
  obtain ‚ü®s, hs, -, hs'‚ü© := LocallyCompactSpace.local_compact_nhds (0 : K) .univ Filter.univ_mem
  obtain ‚ü®r, hr, hr1, H‚ü© :
      ‚àÉ r', r' ‚â† 0 ‚àß valuation K r' < 1 ‚àß { x | valuation K x ‚â§ valuation K r' } ‚äÜ s := by
    obtain ‚ü®r, hr, hrs‚ü© := (IsValuativeTopology.hasBasis_nhds_zero' K).mem_iff.mp hs
    obtain ‚ü®r', hr', hr‚ü© := Valuation.IsNontrivial.exists_lt_one (v := valuation K)
    simp only [ne_eq, map_eq_zero] at hr'
    obtain hr1 | hr1 := lt_or_ge r 1
    ¬∑ obtain ‚ü®r, rfl‚ü© := ValuativeRel.valuation_surjective r
      simp only [ne_eq, map_eq_zero] at hr
      refine ‚ü®r ^ 2, by simpa using hr, by simpa [pow_two], fun x hx ‚Ü¶ hrs ?_‚ü©
      simp only [map_pow, Set.mem_setOf_eq] at hx ‚ä¢
      exact hx.trans_lt (by simpa [pow_two, hr])
    ¬∑ refine ‚ü®r', hr', hr, .trans ?_ hrs‚ü©
      intro x hx
      dsimp at hx ‚ä¢
      exact hx.trans_lt (hr.trans_le hr1)
  convert (hs'.of_isClosed_subset (Valued.isClosed_closedBall K _) H).image
    (Homeomorph.mulLeft‚ÇÄ (Œ≥ / r) (by simp [hr, div_eq_zero_iff, hŒ≥])).continuous using 1
  refine .trans ?_ (Equiv.image_eq_preimage _ _).symm
  ext x
  simp [div_mul_eq_mul_div, div_le_iff‚ÇÄ, IsValuativeTopology.v_eq_valuation, hŒ≥, hr]

instance : CompactSpace ùí™[K] := isCompact_iff_compactSpace.mp (isCompact_closedBall K 1)

instance (K : Type*) [Field K] [ValuativeRel K] [UniformSpace K] [IsUniformAddGroup K]
    [IsValuativeTopology K] : (Valued.v (R := K) (Œì‚ÇÄ := ValueGroupWithZero K)).Compatible :=
  inferInstanceAs (valuation K).Compatible

instance : IsDiscreteValuationRing ùí™[K] :=
  letI := IsTopologicalAddGroup.toUniformSpace K
  haveI := isUniformAddGroup_of_addCommGroup (G := K)
  haveI : CompactSpace (Valued.integer K) := inferInstanceAs (CompactSpace ùí™[K])
  Valued.integer.isDiscreteValuationRing_of_compactSpace

/-- The value group of a local field is (uniquely) isomorphic to `‚Ñ§·µê‚Å∞`. -/
noncomputable
def valueGroupWithZeroIsoInt : ValueGroupWithZero K ‚âÉ*o ‚Ñ§·µê‚Å∞ := by
  apply Nonempty.some
  letI := IsTopologicalAddGroup.toUniformSpace K
  haveI := isUniformAddGroup_of_addCommGroup (G := K)
  obtain ‚ü®_‚ü© := Valued.integer.locallyFiniteOrder_units_mrange_of_isCompact_integer
    (isCompact_iff_compactSpace.mpr (inferInstanceAs (CompactSpace ùí™[K])))
  let e : (MonoidHom.mrange (valuation K)) ‚âÉ*o ValueGroupWithZero K :=
    ‚ü®.ofBijective (MonoidHom.mrange (valuation K)).subtype ‚ü®Subtype.val_injective, fun x ‚Ü¶
      ‚ü®‚ü®x, ValuativeRel.valuation_surjective x‚ü©, rfl‚ü©‚ü©, .rfl‚ü©
  have : Nontrivial (ValueGroupWithZero K)À£ := isNontrivial_iff_nontrivial_units.mp inferInstance
  have : Nontrivial (‚Ü•(MonoidHom.mrange (valuation K)))À£ :=
    (Units.map_injective (f := e.symm.toMonoidHom) e.symm.injective).nontrivial
  exact ‚ü®e.symm.trans (LocallyFiniteOrder.orderMonoidWithZeroEquiv _)‚ü©

instance : ValuativeRel.IsDiscrete K :=
  (ValuativeRel.nonempty_orderIso_withZeroMul_int_iff.mp ‚ü®valueGroupWithZeroIsoInt K‚ü©).1

instance : ValuativeRel.IsRankLeOne K :=
  ValuativeRel.isRankLeOne_iff_mulArchimedean.mpr
    (.comap (valueGroupWithZeroIsoInt K).toMonoidHom (valueGroupWithZeroIsoInt K).strictMono)

instance : Finite ùìÄ[K] :=
  letI := IsTopologicalAddGroup.toUniformSpace K
  haveI := isUniformAddGroup_of_addCommGroup (G := K)
  letI : (Valued.v (R := K)).RankOne :=
    ‚ü®IsRankLeOne.nonempty.some.emb, IsRankLeOne.nonempty.some.strictMono‚ü©
  (compactSpace_iff_completeSpace_and_isDiscreteValuationRing_and_finite_residueField.mp
    (inferInstanceAs (CompactSpace ùí™[K]))).2.2

proof_wanted isAdicComplete : IsAdicComplete ùìÇ[K] ùí™[K]

end TopologicalSpace

section UniformSpace

variable (K : Type*) [Field K] [ValuativeRel K]
  [UniformSpace K] [IsUniformAddGroup K] [IsNonarchimedeanLocalField K]

instance : CompleteSpace K :=
  letI : (Valued.v (R := K)).RankOne :=
    ‚ü®IsRankLeOne.nonempty.some.emb, IsRankLeOne.nonempty.some.strictMono‚ü©
  open scoped Valued in
  have : ProperSpace K := .of_nontriviallyNormedField_of_weaklyLocallyCompactSpace K
  (properSpace_iff_completeSpace_and_isDiscreteValuationRing_integer_and_finite_residueField.mp
    inferInstance).1

instance : CompleteSpace ùí™[K] :=
  letI : (Valued.v (R := K)).RankOne :=
    ‚ü®IsRankLeOne.nonempty.some.emb, IsRankLeOne.nonempty.some.strictMono‚ü©
  (compactSpace_iff_completeSpace_and_isDiscreteValuationRing_and_finite_residueField.mp
    (inferInstanceAs (CompactSpace ùí™[K]))).1

end UniformSpace

end IsNonarchimedeanLocalField
