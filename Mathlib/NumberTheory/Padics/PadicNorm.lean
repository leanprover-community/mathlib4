/-
Copyright (c) 2018 Robert Y. Lewis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robert Y. Lewis
-/
import Mathlib.Algebra.Order.Field.Power
import Mathlib.NumberTheory.Padics.PadicVal

#align_import number_theory.padics.padic_norm from "leanprover-community/mathlib"@"92ca63f0fb391a9ca5f22d2409a6080e786d99f7"

/-!
# p-adic norm

This file defines the `p`-adic norm on `â„š`.

The `p`-adic valuation on `â„š` is the difference of the multiplicities of `p` in the numerator and
denominator of `q`. This function obeys the standard properties of a valuation, with the appropriate
assumptions on `p`.

The valuation induces a norm on `â„š`. This norm is a nonarchimedean absolute value.
It takes values in {0} âˆª {1/p^k | k âˆˆ â„¤}.

## Implementation notes

Much, but not all, of this file assumes that `p` is prime. This assumption is inferred automatically
by taking `[Fact p.Prime]` as a type class argument.

## References

* [F. Q. GouvÃªa, *p-adic numbers*][gouvea1997]
* [R. Y. Lewis, *A formal proof of Hensel's lemma over the p-adic integers*][lewis2019]
* <https://en.wikipedia.org/wiki/P-adic_number>

## Tags

p-adic, p adic, padic, norm, valuation
-/


/-- If `q â‰  0`, the `p`-adic norm of a rational `q` is `p ^ (-padicValRat p q)`.
If `q = 0`, the `p`-adic norm of `q` is `0`. -/
def padicNorm (p : â„•) (q : â„š) : â„š :=
  if q = 0 then 0 else (p : â„š) ^ (-padicValRat p q)
#align padic_norm padicNorm

namespace padicNorm

open padicValRat

variable {p : â„•}

/-- Unfolds the definition of the `p`-adic norm of `q` when `q â‰  0`. -/
@[simp]
protected theorem eq_zpow_of_nonzero {q : â„š} (hq : q â‰  0) :
    padicNorm p q = (p : â„š) ^ (-padicValRat p q) := by simp [hq, padicNorm]
                                                       -- ðŸŽ‰ no goals
#align padic_norm.eq_zpow_of_nonzero padicNorm.eq_zpow_of_nonzero

/-- The `p`-adic norm is nonnegative. -/
protected theorem nonneg (q : â„š) : 0 â‰¤ padicNorm p q :=
  if hq : q = 0 then by simp [hq, padicNorm]
                        -- ðŸŽ‰ no goals
  else by
    unfold padicNorm
    -- âŠ¢ 0 â‰¤ if q = 0 then 0 else â†‘p ^ (-padicValRat p q)
    split_ifs
    -- âŠ¢ 0 â‰¤ â†‘p ^ (-padicValRat p q)
    apply zpow_nonneg
    -- âŠ¢ 0 â‰¤ â†‘p
    exact_mod_cast Nat.zero_le _
    -- ðŸŽ‰ no goals
#align padic_norm.nonneg padicNorm.nonneg

/-- The `p`-adic norm of `0` is `0`. -/
@[simp]
protected theorem zero : padicNorm p 0 = 0 := by simp [padicNorm]
                                                 -- ðŸŽ‰ no goals
#align padic_norm.zero padicNorm.zero

/-- The `p`-adic norm of `1` is `1`. -/
-- @[simp] -- Porting note: simp can prove this
protected theorem one : padicNorm p 1 = 1 := by simp [padicNorm]
                                                -- ðŸŽ‰ no goals
#align padic_norm.one padicNorm.one

/-- The `p`-adic norm of `p` is `pâ»Â¹` if `p > 1`.

See also `padicNorm.padicNorm_p_of_prime` for a version assuming `p` is prime. -/
theorem padicNorm_p (hp : 1 < p) : padicNorm p p = (p : â„š)â»Â¹ := by
  simp [padicNorm, (pos_of_gt hp).ne', padicValNat.self hp]
  -- ðŸŽ‰ no goals
#align padic_norm.padic_norm_p padicNorm.padicNorm_p

/-- The `p`-adic norm of `p` is `pâ»Â¹` if `p` is prime.

See also `padicNorm.padicNorm_p` for a version assuming `1 < p`. -/
@[simp]
theorem padicNorm_p_of_prime [Fact p.Prime] : padicNorm p p = (p : â„š)â»Â¹ :=
  padicNorm_p <| Nat.Prime.one_lt Fact.out
#align padic_norm.padic_norm_p_of_prime padicNorm.padicNorm_p_of_prime

/-- The `p`-adic norm of `q` is `1` if `q` is prime and not equal to `p`. -/
theorem padicNorm_of_prime_of_ne {q : â„•} [p_prime : Fact p.Prime] [q_prime : Fact q.Prime]
    (neq : p â‰  q) : padicNorm p q = 1 := by
  have p : padicValRat p q = 0 := by exact_mod_cast padicValNat_primes neq
  -- âŠ¢ padicNorm pâœ â†‘q = 1
  rw [padicNorm, p]
  -- âŠ¢ (if â†‘q = 0 then 0 else â†‘pâœ ^ (-0)) = 1
  simp [q_prime.1.ne_zero]
  -- ðŸŽ‰ no goals
#align padic_norm.padic_norm_of_prime_of_ne padicNorm.padicNorm_of_prime_of_ne

/-- The `p`-adic norm of `p` is less than `1` if `1 < p`.

See also `padicNorm.padicNorm_p_lt_one_of_prime` for a version assuming `p` is prime. -/
theorem padicNorm_p_lt_one (hp : 1 < p) : padicNorm p p < 1 := by
  rw [padicNorm_p hp, inv_lt_one_iff]
  -- âŠ¢ â†‘p â‰¤ 0 âˆ¨ 1 < â†‘p
  exact_mod_cast Or.inr hp
  -- ðŸŽ‰ no goals
#align padic_norm.padic_norm_p_lt_one padicNorm.padicNorm_p_lt_one

/-- The `p`-adic norm of `p` is less than `1` if `p` is prime.

See also `padicNorm.padicNorm_p_lt_one` for a version assuming `1 < p`. -/
theorem padicNorm_p_lt_one_of_prime [Fact p.Prime] : padicNorm p p < 1 :=
  padicNorm_p_lt_one <| Nat.Prime.one_lt Fact.out
#align padic_norm.padic_norm_p_lt_one_of_prime padicNorm.padicNorm_p_lt_one_of_prime

/-- `padicNorm p q` takes discrete values `p ^ -z` for `z : â„¤`. -/
protected theorem values_discrete {q : â„š} (hq : q â‰  0) : âˆƒ z : â„¤, padicNorm p q = (p : â„š) ^ (-z) :=
  âŸ¨padicValRat p q, by simp [padicNorm, hq]âŸ©
                       -- ðŸŽ‰ no goals
#align padic_norm.values_discrete padicNorm.values_discrete

/-- `padicNorm p` is symmetric. -/
@[simp]
protected theorem neg (q : â„š) : padicNorm p (-q) = padicNorm p q :=
  if hq : q = 0 then by simp [hq] else by simp [padicNorm, hq]
                        -- ðŸŽ‰ no goals
                                          -- ðŸŽ‰ no goals
#align padic_norm.neg padicNorm.neg

variable [hp : Fact p.Prime]

/-- If `q â‰  0`, then `padicNorm p q â‰  0`. -/
protected theorem nonzero {q : â„š} (hq : q â‰  0) : padicNorm p q â‰  0 := by
  rw [padicNorm.eq_zpow_of_nonzero hq]
  -- âŠ¢ â†‘p ^ (-padicValRat p q) â‰  0
  apply zpow_ne_zero_of_ne_zero
  -- âŠ¢ â†‘p â‰  0
  exact_mod_cast ne_of_gt hp.1.pos
  -- ðŸŽ‰ no goals
#align padic_norm.nonzero padicNorm.nonzero

/-- If the `p`-adic norm of `q` is 0, then `q` is `0`. -/
theorem zero_of_padicNorm_eq_zero {q : â„š} (h : padicNorm p q = 0) : q = 0 := by
  apply by_contradiction; intro hq
  -- âŠ¢ Â¬q = 0 â†’ False
                          -- âŠ¢ False
  unfold padicNorm at h; rw [if_neg hq] at h
  -- âŠ¢ False
                         -- âŠ¢ False
  apply absurd h
  -- âŠ¢ Â¬â†‘p ^ (-padicValRat p q) = 0
  apply zpow_ne_zero_of_ne_zero
  -- âŠ¢ â†‘p â‰  0
  exact_mod_cast hp.1.ne_zero
  -- ðŸŽ‰ no goals
#align padic_norm.zero_of_padic_norm_eq_zero padicNorm.zero_of_padicNorm_eq_zero

/-- The `p`-adic norm is multiplicative. -/
@[simp]
protected theorem mul (q r : â„š) : padicNorm p (q * r) = padicNorm p q * padicNorm p r :=
  if hq : q = 0 then by simp [hq]
                        -- ðŸŽ‰ no goals
  else
    if hr : r = 0 then by simp [hr]
                          -- ðŸŽ‰ no goals
    else by
      have : (p : â„š) â‰  0 := by simp [hp.1.ne_zero]
      -- âŠ¢ padicNorm p (q * r) = padicNorm p q * padicNorm p r
      simp [padicNorm, *, padicValRat.mul, zpow_addâ‚€ this, mul_comm]
      -- ðŸŽ‰ no goals
#align padic_norm.mul padicNorm.mul

/-- The `p`-adic norm respects division. -/
@[simp]
protected theorem div (q r : â„š) : padicNorm p (q / r) = padicNorm p q / padicNorm p r :=
  if hr : r = 0 then by simp [hr]
                        -- ðŸŽ‰ no goals
  else eq_div_of_mul_eq (padicNorm.nonzero hr) (by rw [â† padicNorm.mul, div_mul_cancel _ hr])
                                                   -- ðŸŽ‰ no goals
#align padic_norm.div padicNorm.div

/-- The `p`-adic norm of an integer is at most `1`. -/
protected theorem of_int (z : â„¤) : padicNorm p z â‰¤ 1 :=
  if hz : z = 0 then by simp [hz, zero_le_one]
                        -- ðŸŽ‰ no goals
  else by
    unfold padicNorm
    -- âŠ¢ (if â†‘z = 0 then 0 else â†‘p ^ (-padicValRat p â†‘z)) â‰¤ 1
    rw [if_neg _]
    -- âŠ¢ â†‘p ^ (-padicValRat p â†‘z) â‰¤ 1
    Â· refine' zpow_le_one_of_nonpos _ _
      -- âŠ¢ 1 â‰¤ â†‘p
      Â· exact_mod_cast le_of_lt hp.1.one_lt
        -- ðŸŽ‰ no goals
      Â· rw [padicValRat.of_int, neg_nonpos]
        -- âŠ¢ 0 â‰¤ â†‘(padicValInt p z)
        norm_cast
        -- âŠ¢ 0 â‰¤ padicValInt p z
        simp
        -- ðŸŽ‰ no goals
    exact_mod_cast hz
    -- ðŸŽ‰ no goals
#align padic_norm.of_int padicNorm.of_int

private theorem nonarchimedean_aux {q r : â„š} (h : padicValRat p q â‰¤ padicValRat p r) :
    padicNorm p (q + r) â‰¤ max (padicNorm p q) (padicNorm p r) :=
  have hnqp : padicNorm p q â‰¥ 0 := padicNorm.nonneg _
  have hnrp : padicNorm p r â‰¥ 0 := padicNorm.nonneg _
  if hq : q = 0 then by simp [hq, max_eq_right hnrp, le_max_right]
                        -- ðŸŽ‰ no goals
  else
    if hr : r = 0 then by simp [hr, max_eq_left hnqp, le_max_left]
                          -- ðŸŽ‰ no goals
    else
      if hqr : q + r = 0 then le_trans (by simpa [hqr] using hnqp) (le_max_left _ _)
                                           -- ðŸŽ‰ no goals
      else by
        unfold padicNorm; split_ifs
        -- âŠ¢ (if q + r = 0 then 0 else â†‘p ^ (-padicValRat p (q + r))) â‰¤ max (if q = 0 the â€¦
                          -- âŠ¢ â†‘p ^ (-padicValRat p (q + r)) â‰¤ max (â†‘p ^ (-padicValRat p q)) (â†‘p ^ (-padicV â€¦
        apply le_max_iff.2
        -- âŠ¢ â†‘p ^ (-padicValRat p (q + r)) â‰¤ â†‘p ^ (-padicValRat p q) âˆ¨ â†‘p ^ (-padicValRat â€¦
        left
        -- âŠ¢ â†‘p ^ (-padicValRat p (q + r)) â‰¤ â†‘p ^ (-padicValRat p q)
        apply zpow_le_of_le
        -- âŠ¢ 1 â‰¤ â†‘p
        Â· exact_mod_cast le_of_lt hp.1.one_lt
          -- ðŸŽ‰ no goals
        Â· apply neg_le_neg
          -- âŠ¢ padicValRat p q â‰¤ padicValRat p (q + r)
          have : padicValRat p q = min (padicValRat p q) (padicValRat p r) := (min_eq_left h).symm
          -- âŠ¢ padicValRat p q â‰¤ padicValRat p (q + r)
          rw [this]
          -- âŠ¢ min (padicValRat p q) (padicValRat p r) â‰¤ padicValRat p (q + r)
          exact min_le_padicValRat_add hqr
          -- ðŸŽ‰ no goals

/-- The `p`-adic norm is nonarchimedean: the norm of `p + q` is at most the max of the norm of `p`
and the norm of `q`. -/
protected theorem nonarchimedean {q r : â„š} :
    padicNorm p (q + r) â‰¤ max (padicNorm p q) (padicNorm p r) := by
  wlog hle : padicValRat p q â‰¤ padicValRat p r generalizing q r
  -- âŠ¢ padicNorm p (q + r) â‰¤ max (padicNorm p q) (padicNorm p r)
  Â· rw [add_comm, max_comm]
    -- âŠ¢ padicNorm p (r + q) â‰¤ max (padicNorm p r) (padicNorm p q)
    exact this (le_of_not_le hle)
    -- ðŸŽ‰ no goals
  exact nonarchimedean_aux hle
  -- ðŸŽ‰ no goals
#align padic_norm.nonarchimedean padicNorm.nonarchimedean

/-- The `p`-adic norm respects the triangle inequality: the norm of `p + q` is at most the norm of
`p` plus the norm of `q`. -/
theorem triangle_ineq (q r : â„š) : padicNorm p (q + r) â‰¤ padicNorm p q + padicNorm p r :=
  calc
    padicNorm p (q + r) â‰¤ max (padicNorm p q) (padicNorm p r) := padicNorm.nonarchimedean
    _ â‰¤ padicNorm p q + padicNorm p r :=
      max_le_add_of_nonneg (padicNorm.nonneg _) (padicNorm.nonneg _)
#align padic_norm.triangle_ineq padicNorm.triangle_ineq

/-- The `p`-adic norm of a difference is at most the max of each component. Restates the archimedean
property of the `p`-adic norm. -/
protected theorem sub {q r : â„š} : padicNorm p (q - r) â‰¤ max (padicNorm p q) (padicNorm p r) := by
  rw [sub_eq_add_neg, â† padicNorm.neg r]
  -- âŠ¢ padicNorm p (q + -r) â‰¤ max (padicNorm p q) (padicNorm p (-r))
  exact padicNorm.nonarchimedean
  -- ðŸŽ‰ no goals
#align padic_norm.sub padicNorm.sub

/-- If the `p`-adic norms of `q` and `r` are different, then the norm of `q + r` is equal to the max
of the norms of `q` and `r`. -/
theorem add_eq_max_of_ne {q r : â„š} (hne : padicNorm p q â‰  padicNorm p r) :
    padicNorm p (q + r) = max (padicNorm p q) (padicNorm p r) := by
  wlog hlt : padicNorm p r < padicNorm p q
  -- âŠ¢ padicNorm p (q + r) = max (padicNorm p q) (padicNorm p r)
  Â· rw [add_comm, max_comm]
    -- âŠ¢ padicNorm p (r + q) = max (padicNorm p r) (padicNorm p q)
    exact this hne.symm (hne.lt_or_lt.resolve_right hlt)
    -- ðŸŽ‰ no goals
  have : padicNorm p q â‰¤ max (padicNorm p (q + r)) (padicNorm p r) :=
    calc
      padicNorm p q = padicNorm p (q + r + (-r)) := by ring_nf
      _ â‰¤ max (padicNorm p (q + r)) (padicNorm p (-r)) := padicNorm.nonarchimedean
      _ = max (padicNorm p (q + r)) (padicNorm p r) := by simp
  have hnge : padicNorm p r â‰¤ padicNorm p (q + r) := by
    apply le_of_not_gt
    intro hgt
    rw [max_eq_right_of_lt hgt] at this
    exact not_lt_of_ge this hlt
  have : padicNorm p q â‰¤ padicNorm p (q + r) := by rwa [max_eq_left hnge] at this
  -- âŠ¢ padicNorm p (q + r) = max (padicNorm p q) (padicNorm p r)
  apply _root_.le_antisymm
  -- âŠ¢ padicNorm p (q + r) â‰¤ max (padicNorm p q) (padicNorm p r)
  Â· apply padicNorm.nonarchimedean
    -- ðŸŽ‰ no goals
  Â· rwa [max_eq_left_of_lt hlt]
    -- ðŸŽ‰ no goals
#align padic_norm.add_eq_max_of_ne padicNorm.add_eq_max_of_ne

/-- The `p`-adic norm is an absolute value: positive-definite and multiplicative, satisfying the
triangle inequality. -/
instance : IsAbsoluteValue (padicNorm p)
    where
  abv_nonneg' := padicNorm.nonneg
  abv_eq_zero' := âŸ¨zero_of_padicNorm_eq_zero, fun hx â†¦ by simp [hx]âŸ©
                                                          -- ðŸŽ‰ no goals
  abv_add' := padicNorm.triangle_ineq
  abv_mul' := padicNorm.mul

theorem dvd_iff_norm_le {n : â„•} {z : â„¤} : â†‘(p ^ n) âˆ£ z â†” padicNorm p z â‰¤ (p : â„š) ^ (-n : â„¤) := by
  unfold padicNorm; split_ifs with hz
  -- âŠ¢ â†‘(p ^ n) âˆ£ z â†” (if â†‘z = 0 then 0 else â†‘p ^ (-padicValRat p â†‘z)) â‰¤ â†‘p ^ (-â†‘n)
                    -- âŠ¢ â†‘(p ^ n) âˆ£ z â†” 0 â‰¤ â†‘p ^ (-â†‘n)
  Â· norm_cast at hz
    -- âŠ¢ â†‘(p ^ n) âˆ£ z â†” 0 â‰¤ â†‘p ^ (-â†‘n)
    simp [hz]
    -- ðŸŽ‰ no goals
  Â· rw [zpow_le_iff_le, neg_le_neg_iff, padicValRat.of_int,
      padicValInt.of_ne_one_ne_zero hp.1.ne_one _]
    Â· norm_cast
      -- âŠ¢ â†‘(p ^ n) âˆ£ z â†” n â‰¤ Part.get (multiplicity (â†‘p) z) (_ : multiplicity.Finite ( â€¦
      rw [â† PartENat.coe_le_coe, PartENat.natCast_get, â† multiplicity.pow_dvd_iff_le_multiplicity,
        Nat.cast_pow]
      exact_mod_cast hz
      -- ðŸŽ‰ no goals
    Â· exact_mod_cast hp.1.one_lt
      -- ðŸŽ‰ no goals
#align padic_norm.dvd_iff_norm_le padicNorm.dvd_iff_norm_le

/-- The `p`-adic norm of an integer `m` is one iff `p` doesn't divide `m`. -/
theorem int_eq_one_iff (m : â„¤) : padicNorm p m = 1 â†” Â¬(p : â„¤) âˆ£ m := by
  nth_rw 2 [â† pow_one p]
  -- âŠ¢ padicNorm p â†‘m = 1 â†” Â¬â†‘(p ^ 1) âˆ£ m
  simp only [dvd_iff_norm_le, Int.cast_ofNat, Nat.cast_one, zpow_neg, zpow_one, not_le]
  -- âŠ¢ padicNorm p â†‘m = 1 â†” (â†‘p)â»Â¹ < padicNorm p â†‘m
  constructor
  -- âŠ¢ padicNorm p â†‘m = 1 â†’ (â†‘p)â»Â¹ < padicNorm p â†‘m
  Â· intro h
    -- âŠ¢ (â†‘p)â»Â¹ < padicNorm p â†‘m
    rw [h, inv_lt_one_iff_of_pos] <;> norm_cast
    -- âŠ¢ 1 < â†‘p
                                      -- âŠ¢ 1 < p
                                      -- âŠ¢ 0 < p
    Â· exact Nat.Prime.one_lt Fact.out
      -- ðŸŽ‰ no goals
    Â· exact Nat.Prime.pos Fact.out
      -- ðŸŽ‰ no goals
  Â· simp only [padicNorm]
    -- âŠ¢ ((â†‘p)â»Â¹ < if â†‘m = 0 then 0 else â†‘p ^ (-padicValRat p â†‘m)) â†’ (if â†‘m = 0 then  â€¦
    split_ifs
    -- âŠ¢ (â†‘p)â»Â¹ < 0 â†’ 0 = 1
    Â· rw [inv_lt_zero, â† Nat.cast_zero, Nat.cast_lt]
      -- âŠ¢ p < 0 â†’ â†‘0 = 1
      intro h
      -- âŠ¢ â†‘0 = 1
      exact (Nat.not_lt_zero p h).elim
      -- ðŸŽ‰ no goals
    Â· have : 1 < (p : â„š) := by norm_cast; exact Nat.Prime.one_lt (Fact.out : Nat.Prime p)
      -- âŠ¢ (â†‘p)â»Â¹ < â†‘p ^ (-padicValRat p â†‘m) â†’ â†‘p ^ (-padicValRat p â†‘m) = 1
      rw [â† zpow_neg_one, zpow_lt_iff_lt this]
      -- âŠ¢ -1 < -padicValRat p â†‘m â†’ â†‘p ^ (-padicValRat p â†‘m) = 1
      have : 0 â‰¤ padicValRat p m
      -- âŠ¢ 0 â‰¤ padicValRat p â†‘m
      simp only [of_int, Nat.cast_nonneg]
      -- âŠ¢ -1 < -padicValRat p â†‘m â†’ â†‘p ^ (-padicValRat p â†‘m) = 1
      intro h
      -- âŠ¢ â†‘p ^ (-padicValRat p â†‘m) = 1
      rw [â† zpow_zero (p : â„š), zpow_inj] <;> linarith
                                             -- ðŸŽ‰ no goals
                                             -- ðŸŽ‰ no goals
                                             -- ðŸŽ‰ no goals
#align padic_norm.int_eq_one_iff padicNorm.int_eq_one_iff

theorem int_lt_one_iff (m : â„¤) : padicNorm p m < 1 â†” (p : â„¤) âˆ£ m := by
  rw [â† not_iff_not, â† int_eq_one_iff, eq_iff_le_not_lt]
  -- âŠ¢ Â¬padicNorm p â†‘m < 1 â†” padicNorm p â†‘m â‰¤ 1 âˆ§ Â¬padicNorm p â†‘m < 1
  simp only [padicNorm.of_int, true_and_iff]
  -- ðŸŽ‰ no goals
#align padic_norm.int_lt_one_iff padicNorm.int_lt_one_iff

theorem of_nat (m : â„•) : padicNorm p m â‰¤ 1 :=
  padicNorm.of_int (m : â„¤)
#align padic_norm.of_nat padicNorm.of_nat

/-- The `p`-adic norm of a natural `m` is one iff `p` doesn't divide `m`. -/
theorem nat_eq_one_iff (m : â„•) : padicNorm p m = 1 â†” Â¬p âˆ£ m := by
  rw [â† Int.coe_nat_dvd, â† int_eq_one_iff, Int.cast_ofNat]
  -- ðŸŽ‰ no goals
#align padic_norm.nat_eq_one_iff padicNorm.nat_eq_one_iff

theorem nat_lt_one_iff (m : â„•) : padicNorm p m < 1 â†” p âˆ£ m := by
  rw [â† Int.coe_nat_dvd, â† int_lt_one_iff, Int.cast_ofNat]
  -- ðŸŽ‰ no goals
#align padic_norm.nat_lt_one_iff padicNorm.nat_lt_one_iff

open BigOperators

theorem sum_lt {Î± : Type*} {F : Î± â†’ â„š} {t : â„š} {s : Finset Î±} :
    s.Nonempty â†’ (âˆ€ i âˆˆ s, padicNorm p (F i) < t) â†’ padicNorm p (âˆ‘ i in s, F i) < t := by
  classical
    refine' s.induction_on (by rintro âŸ¨-, âŸ¨âŸ©âŸ©) _
    rintro a S haS IH - ht
    by_cases hs : S.Nonempty
    Â· rw [Finset.sum_insert haS]
      exact
        lt_of_le_of_lt padicNorm.nonarchimedean
          (max_lt (ht a (Finset.mem_insert_self a S))
            (IH hs fun b hb â†¦ ht b (Finset.mem_insert_of_mem hb)))
    Â· simp_all
#align padic_norm.sum_lt padicNorm.sum_lt

theorem sum_le {Î± : Type*} {F : Î± â†’ â„š} {t : â„š} {s : Finset Î±} :
    s.Nonempty â†’ (âˆ€ i âˆˆ s, padicNorm p (F i) â‰¤ t) â†’ padicNorm p (âˆ‘ i in s, F i) â‰¤ t := by
  classical
    refine' s.induction_on (by rintro âŸ¨-, âŸ¨âŸ©âŸ©) _
    rintro a S haS IH - ht
    by_cases hs : S.Nonempty
    Â· rw [Finset.sum_insert haS]
      exact
        padicNorm.nonarchimedean.trans
          (max_le (ht a (Finset.mem_insert_self a S))
            (IH hs fun b hb â†¦ ht b (Finset.mem_insert_of_mem hb)))
    Â· simp_all
#align padic_norm.sum_le padicNorm.sum_le

theorem sum_lt' {Î± : Type*} {F : Î± â†’ â„š} {t : â„š} {s : Finset Î±}
    (hF : âˆ€ i âˆˆ s, padicNorm p (F i) < t) (ht : 0 < t) : padicNorm p (âˆ‘ i in s, F i) < t := by
  obtain rfl | hs := Finset.eq_empty_or_nonempty s
  -- âŠ¢ padicNorm p (âˆ‘ i in âˆ…, F i) < t
  Â· simp [ht]
    -- ðŸŽ‰ no goals
  Â· exact sum_lt hs hF
    -- ðŸŽ‰ no goals
#align padic_norm.sum_lt' padicNorm.sum_lt'

theorem sum_le' {Î± : Type*} {F : Î± â†’ â„š} {t : â„š} {s : Finset Î±}
    (hF : âˆ€ i âˆˆ s, padicNorm p (F i) â‰¤ t) (ht : 0 â‰¤ t) : padicNorm p (âˆ‘ i in s, F i) â‰¤ t := by
  obtain rfl | hs := Finset.eq_empty_or_nonempty s
  -- âŠ¢ padicNorm p (âˆ‘ i in âˆ…, F i) â‰¤ t
  Â· simp [ht]
    -- ðŸŽ‰ no goals
  Â· exact sum_le hs hF
    -- ðŸŽ‰ no goals
#align padic_norm.sum_le' padicNorm.sum_le'

end padicNorm
