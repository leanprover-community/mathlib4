/-
Copyright (c) 2025 MarÃ­a InÃ©s de Frutos-FernÃ¡ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: MarÃ­a InÃ©s de Frutos-FernÃ¡ndez
-/
import Mathlib.Analysis.Normed.Unbundled.SpectralNorm
import Mathlib.NumberTheory.Padics.PadicNumbers
import Mathlib.Topology.Algebra.Valued.NormedValued
import Mathlib.Topology.Algebra.Valued.ValuedField

/-!
# The `p`-adic complex numbers.

In this file we define the field `â„‚_[p]` of `p`-adic complex numbers and we give it both a normed
field and a valued field structure, induced by the unique extension of the `p`-adic norm to `â„‚_[p]`.

## Main Definitions
* `PadicAlg p` : the algebraic closure of `â„š_[p]`.
* `PadicComplex p` : the type of `p`-adic complex numbers.
* `PadicComplex_integers` : the ring of integers of `â„‚_[p]`.

## Main Results

* `PadicComplex.norm_extends` : the norm on `â„‚_[p]` extends the norm on `PadicAlg p`, and hence
  the norm on `â„š_[p]`.
* `PadicComplex.isNonarchimedean` : The norm on `â„‚_[p]` is nonarchimedean.

## Notation

We introduce the notation `â„‚_[p]` for the `p`-adic complex numbers, and `ğ“_â„‚_[p]` for its ring of
integers.

## Tags

p-adic, p adic, padic, norm, valuation, Cauchy, completion, p-adic completion
-/

noncomputable section

open Valued Valuation

open scoped NNReal

variable (p : â„•) [hp : Fact (Nat.Prime p)]

/-- `PadicAlg p` is the algebraic closure of `â„š_[p]`. -/
abbrev PadicAlg := AlgebraicClosure â„š_[p]

namespace PadicAlg

/-- `PadicAlg p` is an algebraic extension of `â„š_[p]`. -/
theorem isAlgebraic : Algebra.IsAlgebraic â„š_[p] (PadicAlg p) := AlgebraicClosure.isAlgebraic _

instance : Coe â„š_[p] (PadicAlg p) := âŸ¨algebraMap â„š_[p] (PadicAlg p)âŸ©

theorem coe_eq : (Coe.coe : â„š_[p] â†’ PadicAlg p) = algebraMap â„š_[p] (PadicAlg p) := rfl

/-- `PadicAlg p` is a normed field, where the norm is the `p`-adic norm, that is, the spectral norm
induced by the `p`-adic norm on `â„š_[p]`. -/
instance normedField : NormedField (PadicAlg p) := spectralNorm.normedField â„š_[p] (PadicAlg p)

/-- The norm on `PadicAlg p` is nonarchimedean. -/
theorem isNonarchimedean : IsNonarchimedean (norm : PadicAlg p â†’ â„) :=
  isNonarchimedean_spectralNorm (K := â„š_[p]) (L := PadicAlg p)

/-- The norm on `PadicAlg p` is the spectral norm induced by the `p`-adic norm on `â„š_[p]`. -/
theorem norm_def (x : PadicAlg p) : â€–xâ€– = spectralNorm â„š_[p] (PadicAlg p) x := rfl

/-- The norm on `PadicAlg p` extends the `p`-adic norm on `â„š_[p]`. -/
theorem norm_extends (x : â„š_[p]) : â€–(x : PadicAlg p)â€– = â€–xâ€– :=
  spectralAlgNorm_extends (K := â„š_[p]) (L := PadicAlg p) _

instance : IsUltrametricDist (PadicAlg p) :=
  IsUltrametricDist.isUltrametricDist_of_forall_norm_add_le_max_norm (PadicAlg.isNonarchimedean p)

/-- `PadicAlg p` is a valued field, with the valuation corresponding to the `p`-adic norm. -/
instance valued : Valued (PadicAlg p) â„â‰¥0 := NormedField.toValued

/-- The valuation of `x : PadicAlg p` agrees with its `â„â‰¥0`-valued norm. -/
theorem valuation_def (x : PadicAlg p) : Valued.v x = â€–xâ€–â‚Š := rfl

/-- The coercion of the valuation of `x : PadicAlg p` to `â„` agrees with its norm. -/
theorem valuation_coe (x : PadicAlg p) :
  ((Valued.v x : â„â‰¥0) : â„) = spectralNorm â„š_[p] (PadicAlg p) x := rfl

/-- The valuation of `p : PadicAlg p` is `1/p`. -/
theorem valuation_p (p : â„•) [Fact p.Prime] : Valued.v (p : PadicAlg p) = 1 / (p : â„â‰¥0) := by
  rw [â† map_natCast (algebraMap â„š_[p] (PadicAlg p))]
  ext
  rw [valuation_coe, spectralNorm_extends, padicNormE.norm_p, one_div, NNReal.coe_inv,
    NNReal.coe_natCast]

/-- The valuation on `PadicAlg p` has rank one. -/
instance : RankOne (PadicAlg.valued p).v where
  hom         := MonoidWithZeroHom.id â„â‰¥0
  strictMono' := strictMono_id
  nontrivial' := by
    use p
    haveI hp : Nat.Prime p := hp.1
    simp only [valuation_p, one_div, ne_eq, inv_eq_zero, Nat.cast_eq_zero, inv_eq_one,
      Nat.cast_eq_one]
    exact âŸ¨hp.ne_zero, hp.ne_oneâŸ©

instance : UniformContinuousConstSMul â„š_[p] (PadicAlg p) :=
  uniformContinuousConstSMul_of_continuousConstSMul â„š_[p] (PadicAlg p)

end PadicAlg

/-- `â„‚_[p]` is the field of `p`-adic complex numbers, that is, the completion of `PadicAlg p` with
respect to the `p`-adic norm. -/
abbrev PadicComplex := UniformSpace.Completion (PadicAlg p)

/-- `â„‚_[p]` is the field of `p`-adic complex numbers. -/
notation "â„‚_[" p "]" => PadicComplex p

namespace PadicComplex
/-- `â„‚_[p]` is a valued field, where the valuation is the one extending that on `PadicAlg p`. -/
instance valued : Valued â„‚_[p] â„â‰¥0 := inferInstance

/-- The valuation on `â„‚_[p]` extends the valuation on `PadicAlg p`. -/
theorem valuation_extends (x : PadicAlg p) : Valued.v (x : â„‚_[p]) = Valued.v x :=
  Valued.extension_extends _

theorem coe_eq (x : PadicAlg p) : (x : â„‚_[p]) = algebraMap (PadicAlg p) â„‚_[p] x := rfl

theorem coe_zero : ((0 : PadicAlg p) : â„‚_[p]) = 0 := rfl

/-- `â„‚_[p]` is an algebra over `â„š_[p]`. -/
instance : Algebra â„š_[p] â„‚_[p] where
  smul := (UniformSpace.Completion.instSMul â„š_[p] (PadicAlg p)).smul
  algebraMap :=
    RingHom.comp (UniformSpace.Completion.coeRingHom) (algebraMap â„š_[p] (PadicAlg p))
  commutes' r x := by rw [mul_comm]
  smul_def' r x := by
    apply UniformSpace.Completion.ext' (continuous_const_smul r) (continuous_mul_left _)
    intro a
    have : UniformSpace.Completion.coeRingHom ((algebraMap â„š_[p] (PadicAlg p)) r) * â†‘a =
       UniformSpace.Completion.coeRingHom ((algebraMap â„š_[p] (PadicAlg p)) r) *
       UniformSpace.Completion.coeRingHom a := rfl
    rw [â† UniformSpace.Completion.coe_smul, RingHom.coe_comp, Function.comp_apply,
      this, â† map_mul, Algebra.smul_def]
    rfl

instance : IsScalarTower â„š_[p] (PadicAlg p) â„‚_[p] := IsScalarTower.of_algebraMap_eq (congrFun rfl)

@[simp, norm_cast]
lemma coe_natCast (n : â„•) : ((n : PadicAlg p) : â„‚_[p]) = (n : â„‚_[p])  := by
  rw [â† map_natCast (algebraMap (PadicAlg p) â„‚_[p]) n, coe_eq]

/-- The valuation of `p : â„‚_[p]` is `1/p`. -/
theorem valuation_p : Valued.v (p : â„‚_[p]) = 1 / (p : â„â‰¥0) := by
  have := valuation_extends p (p : PadicAlg p)
  rw [PadicAlg.valuation_p] at this
  simpa only [â† map_natCast (algebraMap (PadicAlg p) â„‚_[p]), â† coe_eq]

/-- The valuation on `â„‚_[p]` has rank one. -/
instance : RankOne (PadicComplex.valued p).v where
  hom         := MonoidWithZeroHom.id â„â‰¥0
  strictMono' := strictMono_id
  nontrivial' := by
    use p
    haveI hp : Nat.Prime p := hp.1
    simp only [valuation_p, one_div, ne_eq, inv_eq_zero, Nat.cast_eq_zero, inv_eq_one,
      Nat.cast_eq_one]
    exact âŸ¨hp.ne_zero, hp.ne_oneâŸ©

lemma rankOne_hom_eq :
    RankOne.hom (PadicComplex.valued p).v = RankOne.hom (PadicAlg.valued p).v := rfl

/-- `â„‚_[p]` is a normed field, where the norm corresponds to the extension of the `p`-adic
  valuation. -/
instance : NormedField â„‚_[p] :=  Valued.toNormedField _ _

theorem norm_def : (Norm.norm : â„‚_[p] â†’ â„) = Valued.norm := rfl

/-- The norm on `â„‚_[p]` extends the norm on `PadicAlg p`. -/
theorem norm_extends (x : PadicAlg p) : â€–(x : â„‚_[p])â€– = â€–xâ€– := by
  rw [norm_def, Valued.norm, â† coe_nnnorm, valuation_extends p x, coe_nnnorm]
  rfl

/-- The `â„â‰¥0`-valued norm on `â„‚_[p]` extends that on `PadicAlg p`. -/
theorem nnnorm_extends (x : PadicAlg p) : â€–(x : â„‚_[p])â€–â‚Š = â€–xâ€–â‚Š := by ext; exact norm_extends p x

/-- The norm on `â„‚_[p]` is nonarchimedean. -/
theorem isNonarchimedean : IsNonarchimedean (Norm.norm : â„‚_[p] â†’ â„) := fun x y â†¦ by
  refine UniformSpace.Completion.induction_onâ‚‚ x y
    (isClosed_le (Continuous.comp continuous_norm continuous_add)
      (Continuous.max (Continuous.comp (@continuous_norm â„‚_[p] _) (Continuous.fst continuous_id))
      (Continuous.comp (@continuous_norm â„‚_[p] _) (Continuous.snd continuous_id)))) (fun a b â†¦ ?_)
  Â· rw [â† UniformSpace.Completion.coe_add, norm_extends, norm_extends, norm_extends]
    exact PadicAlg.isNonarchimedean p a b

end PadicComplex

/-- We define `ğ“_â„‚_[p]` as the valuation subring of of `â„‚_[p]`, consisting of those elements with
  valuation `â‰¤ 1`. -/
def padicComplexIntegers : ValuationSubring â„‚_[p] := (PadicComplex.valued p).v.valuationSubring

/-- We define `ğ“_â„‚_[p]` as the subring of elements of `â„‚_[p]` with valuation `â‰¤ 1`. -/
notation "ğ“_â„‚_[" p "]" => padicComplexIntegers p

/-- `ğ“_â„‚_[p]` is the ring of integers of `â„‚_[p]`. -/
theorem PadicComplex.integers : Valuation.Integers (PadicComplex.valued p).v ğ“_â„‚_[p] :=
  Valuation.integer.integers _
