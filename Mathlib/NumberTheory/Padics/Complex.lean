/-
Copyright (c) 2025 MarÃ­a InÃ©s de Frutos-FernÃ¡ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: MarÃ­a InÃ©s de Frutos-FernÃ¡ndez
-/
import Mathlib.Analysis.Normed.Unbundled.SpectralNorm
import Mathlib.NumberTheory.Padics.PadicNumbers
import Mathlib.Topology.Algebra.Valued.NormedValued
import Mathlib.Topology.Algebra.Valued.ValuedField

/-!
# The field `â„‚_[p]` of `p`-adic complex numbers.

In this file we define the field `â„‚_[p]` of `p`-adic complex numbers as the `p`-adic completion of
an algebraic closure of `â„š_[p]`. We endow `â„‚_[p]` with both a normed field and a valued field
structure, induced by the unique extension of the `p`-adic norm to `â„‚_[p]`.

## Main Definitions
* `PadicAlgCl p` : the algebraic closure of `â„š_[p]`.
* `PadicComplex p` : the type of `p`-adic complex numbers, denoted by `â„‚_[p]`.
* `PadicComplexInt p` : the ring of integers of `â„‚_[p]`.

## Main Results

* `PadicComplex.norm_extends` : the norm on `â„‚_[p]` extends the norm on `PadicAlgCl p`, and hence
  the norm on `â„š_[p]`.
* `PadicComplex.isNonarchimedean` : The norm on `â„‚_[p]` is nonarchimedean.

## Notation

We introduce the notation `â„‚_[p]` for the `p`-adic complex numbers, and `ğ“_â„‚_[p]` for its ring of
integers.

## Tags

p-adic, p adic, padic, norm, valuation, Cauchy, completion, p-adic completion
-/

noncomputable section

open Valuation

open scoped NNReal

variable (p : â„•) [hp : Fact (Nat.Prime p)]

/-- `PadicAlgCl p` is a fixed algebraic closure of `â„š_[p]`. -/
abbrev PadicAlgCl := AlgebraicClosure â„š_[p]

namespace PadicAlgCl

/-- `PadicAlgCl p` is an algebraic extension of `â„š_[p]`. -/
theorem isAlgebraic : Algebra.IsAlgebraic â„š_[p] (PadicAlgCl p) := AlgebraicClosure.isAlgebraic _

instance : Coe â„š_[p] (PadicAlgCl p) := âŸ¨algebraMap â„š_[p] (PadicAlgCl p)âŸ©

theorem coe_eq : (Coe.coe : â„š_[p] â†’ PadicAlgCl p) = algebraMap â„š_[p] (PadicAlgCl p) := rfl

/-- `PadicAlgCl p` is a normed field, where the norm is the `p`-adic norm, that is, the
spectral norm induced by the `p`-adic norm on `â„š_[p]`. -/
instance normedField : NormedField (PadicAlgCl p) := spectralNorm.normedField â„š_[p] (PadicAlgCl p)

/-- The norm on `PadicAlgCl p` is nonarchimedean. -/
theorem isNonarchimedean : IsNonarchimedean (norm : PadicAlgCl p â†’ â„) :=
  isNonarchimedean_spectralNorm (K := â„š_[p]) (L := PadicAlgCl p)

/-- The norm on `PadicAlgCl p` is the spectral norm induced by the `p`-adic norm on `â„š_[p]`. -/
@[simp]
theorem spectralNorm_eq (x : PadicAlgCl p) : spectralNorm â„š_[p] (PadicAlgCl p) x = â€–xâ€– := rfl

/-- The norm on `PadicAlgCl p` extends the `p`-adic norm on `â„š_[p]`. -/
@[simp] theorem norm_extends (x : â„š_[p]) : â€–(x : PadicAlgCl p)â€– = â€–xâ€– :=
  spectralAlgNorm_extends (K := â„š_[p]) (L := PadicAlgCl p) _

instance : IsUltrametricDist (PadicAlgCl p) :=
  IsUltrametricDist.isUltrametricDist_of_forall_norm_add_le_max_norm (PadicAlgCl.isNonarchimedean p)

/-- `PadicAlgCl p` is a valued field, with the valuation corresponding to the `p`-adic norm. -/
instance valued : Valued (PadicAlgCl p) â„â‰¥0 := NormedField.toValued

/-- The valuation of `x : PadicAlgCl p` agrees with its `â„â‰¥0`-valued norm. -/
theorem valuation_def (x : PadicAlgCl p) : Valued.v x = â€–xâ€–â‚Š := rfl

/-- The coercion of the valuation of `x : PadicAlgCl p` to `â„` agrees with its norm. -/
@[simp] theorem valuation_coe (x : PadicAlgCl p) : ((Valued.v x : â„â‰¥0) : â„) = â€–xâ€– := rfl

/-- The valuation of `p : PadicAlgCl p` is `1/p`. -/
theorem valuation_p (p : â„•) [Fact p.Prime] : Valued.v (p : PadicAlgCl p) = 1 / (p : â„â‰¥0) := by
  rw [â† map_natCast (algebraMap â„š_[p] (PadicAlgCl p))]
  ext
  rw [valuation_coe, norm_extends, padicNormE.norm_p, one_div, NNReal.coe_inv,
    NNReal.coe_natCast]

/-- The valuation on `PadicAlgCl p` has rank one. -/
instance : RankOne (PadicAlgCl.valued p).v where
  hom         := MonoidWithZeroHom.id â„â‰¥0
  strictMono' := strictMono_id
  exists_val_nontrivial := by
    use p
    have hp : Nat.Prime p := hp.1
    simp only [valuation_p, one_div, ne_eq, inv_eq_zero, Nat.cast_eq_zero, inv_eq_one,
      Nat.cast_eq_one]
    exact âŸ¨hp.ne_zero, hp.ne_oneâŸ©

instance : UniformContinuousConstSMul â„š_[p] (PadicAlgCl p) :=
  uniformContinuousConstSMul_of_continuousConstSMul â„š_[p] (PadicAlgCl p)

end PadicAlgCl

/-- `â„‚_[p]` is the field of `p`-adic complex numbers, that is, the completion of `PadicAlgCl p` with
respect to the `p`-adic norm. -/
abbrev PadicComplex := UniformSpace.Completion (PadicAlgCl p)

/-- `â„‚_[p]` is the field of `p`-adic complex numbers. -/
notation "â„‚_[" p "]" => PadicComplex p

namespace PadicComplex
/-- `â„‚_[p]` is a valued field, where the valuation is the one extending that on `PadicAlgCl p`. -/
instance valued : Valued â„‚_[p] â„â‰¥0 := inferInstance

/-- The valuation on `â„‚_[p]` extends the valuation on `PadicAlgCl p`. -/
theorem valuation_extends (x : PadicAlgCl p) : Valued.v (x : â„‚_[p]) = Valued.v x :=
  Valued.extension_extends _

theorem coe_eq (x : PadicAlgCl p) : (x : â„‚_[p]) = algebraMap (PadicAlgCl p) â„‚_[p] x := rfl

@[simp] theorem coe_zero : ((0 : PadicAlgCl p) : â„‚_[p]) = 0 := rfl

/-- `â„‚_[p]` is an algebra over `â„š_[p]`. -/
instance : Algebra â„š_[p] â„‚_[p] where
  smul := (UniformSpace.Completion.instSMul â„š_[p] (PadicAlgCl p)).smul
  algebraMap := (UniformSpace.Completion.coeRingHom).comp (algebraMap â„š_[p] (PadicAlgCl p))
  commutes' r x := by rw [mul_comm]
  smul_def' r x := by
    apply UniformSpace.Completion.ext' (continuous_const_smul r) (continuous_mul_left _)
    intro a
    rw [RingHom.coe_comp, Function.comp_apply, Algebra.smul_def]
    rfl

instance : IsScalarTower â„š_[p] (PadicAlgCl p) â„‚_[p] := IsScalarTower.of_algebraMap_eq (congrFun rfl)

@[simp, norm_cast]
lemma coe_natCast (n : â„•) : ((n : PadicAlgCl p) : â„‚_[p]) = (n : â„‚_[p]) := by
  rw [â† map_natCast (algebraMap (PadicAlgCl p) â„‚_[p]) n, coe_eq]

/-- The valuation of `p : â„‚_[p]` is `1/p`. -/
theorem valuation_p : Valued.v (p : â„‚_[p]) = 1 / (p : â„â‰¥0) := by
  rw [â† map_natCast (algebraMap (PadicAlgCl p) â„‚_[p]), â† coe_eq, valuation_extends,
    PadicAlgCl.valuation_p]

/-- The valuation on `â„‚_[p]` has rank one. -/
instance : RankOne (PadicComplex.valued p).v where
  hom         := MonoidWithZeroHom.id â„â‰¥0
  strictMono' := strictMono_id
  exists_val_nontrivial := by
    use p
    have hp : Nat.Prime p := hp.1
    simp only [valuation_p, one_div, ne_eq, inv_eq_zero, Nat.cast_eq_zero, inv_eq_one,
      Nat.cast_eq_one]
    exact âŸ¨hp.ne_zero, hp.ne_oneâŸ©

lemma rankOne_hom_eq :
    RankOne.hom (PadicComplex.valued p).v = RankOne.hom (PadicAlgCl.valued p).v := rfl

/-- `â„‚_[p]` is a normed field, where the norm corresponds to the extension of the `p`-adic
  valuation. -/
instance : NormedField â„‚_[p] := Valued.toNormedField _ _

theorem norm_def : (Norm.norm : â„‚_[p] â†’ â„) = Valued.norm := rfl

/-- The norm on `â„‚_[p]` extends the norm on `PadicAlgCl p`. -/
theorem norm_extends (x : PadicAlgCl p) : â€–(x : â„‚_[p])â€– = â€–xâ€– := by
  rw [norm_def, Valued.norm, â† coe_nnnorm, valuation_extends p x, coe_nnnorm]
  rfl

/-- The `â„â‰¥0`-valued norm on `â„‚_[p]` extends that on `PadicAlgCl p`. -/
theorem nnnorm_extends (x : PadicAlgCl p) : â€–(x : â„‚_[p])â€–â‚Š = â€–xâ€–â‚Š := by ext; exact norm_extends p x

/-- The norm on `â„‚_[p]` is nonarchimedean. -/
theorem isNonarchimedean : IsNonarchimedean (Norm.norm : â„‚_[p] â†’ â„) := fun x y â†¦ by
  refine UniformSpace.Completion.induction_onâ‚‚ x y
    (isClosed_le (continuous_norm.comp continuous_add) (by fun_prop)) (fun a b â†¦ ?_)
  rw [â† UniformSpace.Completion.coe_add, norm_extends, norm_extends, norm_extends]
  exact PadicAlgCl.isNonarchimedean p a b

end PadicComplex

/-- We define `ğ“_â„‚_[p]` as the valuation subring of of `â„‚_[p]`, consisting of those elements with
  valuation `â‰¤ 1`. -/
def PadicComplexInt : ValuationSubring â„‚_[p] := (PadicComplex.valued p).v.valuationSubring

/-- We define `ğ“_â„‚_[p]` as the subring of elements of `â„‚_[p]` with valuation `â‰¤ 1`. -/
notation "ğ“_â„‚_[" p "]" => PadicComplexInt p

/-- `ğ“_â„‚_[p]` is the ring of integers of `â„‚_[p]`. -/
theorem PadicComplexInt.integers : Valuation.Integers (PadicComplex.valued p).v ğ“_â„‚_[p] :=
  Valuation.integer.integers _
