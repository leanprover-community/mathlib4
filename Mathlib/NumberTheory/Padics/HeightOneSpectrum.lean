/-
Copyright (c) 2025 Salvatore Mercuri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Salvatore Mercuri
-/
module

public import Mathlib.NumberTheory.Padics.WithVal
public import Mathlib.RingTheory.DedekindDomain.AdicValuation
public import Mathlib.RingTheory.Int.Basic

/-!
# Isomorphisms between `adicCompletion â„š` and `â„š_[p]`

If `v : HeightOneSpectrum â„š`, then `v.adicCompletion â„š` is the uniform space completion of `â„š`
with respect to the `v`-adic valuation, which definition generalises to Dedekind domains and
their field of fractions. On the other hand, `â„š_[p]` is the `p`-adic numbers, defined as the
completion of `â„š` with respect to the `p`-adic norm using the completion of Cauchy sequences.
This file constructs uniform and `â„š`-algebra` isomorphisms between the two, as well as for their
respective rings of integers.

## Main definitions
- `Rat.HeightOneSpectrum.natGenerator v` : the generator in `â„•` of a height-one prime ideal
  in `ğ“ â„š`.
- `Rat.HeightOneSpectrum.padicUniformEquiv v` : `v.adicCompletion â„š â‰ƒáµ¤ â„š_[natGenerator v]`.
- `Rat.HeightOneSpectrum.padicAlgEquiv v` : `v.adicCompletion â„š â‰ƒâ‚[â„š] â„š_[natGenerator v]`.
- `Rat.HeightOneSpectrum.adicCompletionIntegers.padicIntUniformEquiv v` :
  `v.adicCompletionIntegers â„š â‰ƒáµ¤ â„¤_[natGenerator v]`.
- `Rat.HeightOneSpectrum.adicCompletionIntegers.padicIntRingEquiv v` :
  `v.adicCompletionIntegers â„š â‰ƒ+* â„¤_[natGenerator v]`.
-/

@[expose] public section

open IsDedekindDomain UniformSpace.Completion NumberField PadicInt

namespace Rat.HeightOneSpectrum

/-- The generator in `â„•` of a height-one prime ideal in `ğ“ â„š`. -/
noncomputable def natGenerator (v : HeightOneSpectrum (ğ“ â„š)) : â„• :=
  Submodule.IsPrincipal.generator (v.asIdeal.map ringOfIntegersEquiv) |>.natAbs

theorem span_natGenerator (v : HeightOneSpectrum (ğ“ â„š)) :
    Ideal.span {(natGenerator v : â„¤)} = v.asIdeal.map ringOfIntegersEquiv := by
  simp [natGenerator]

theorem natGenerator_dvd_iff (v : HeightOneSpectrum (ğ“ â„š)) {n : â„•} :
    natGenerator v âˆ£ n â†” â†‘n âˆˆ v.asIdeal.map Rat.ringOfIntegersEquiv := by
  rw [â† span_natGenerator, Ideal.mem_span_singleton]
  exact Int.ofNat_dvd.symm

local instance (v : HeightOneSpectrum (ğ“ â„š)) : Fact (Nat.Prime (natGenerator v)) :=
  âŸ¨Int.prime_iff_natAbs_prime.1 <| Submodule.IsPrincipal.prime_generator_of_isPrime _
    ((Ideal.map_eq_bot_iff_of_injective ringOfIntegersEquiv.injective).not.2 v.ne_bot)âŸ©

theorem valuation_equiv_padicValuation (v : HeightOneSpectrum (ğ“ â„š)) :
    (v.valuation â„š).IsEquiv (Rat.padicValuation (natGenerator v)) := by
  simp [Valuation.isEquiv_iff_val_le_one, padicValuation_le_one_iff, valuation_le_one_iff_den,
    natGenerator_dvd_iff, â† Ideal.apply_mem_of_equiv_iff (f := ringOfIntegersEquiv)]

/-- The uniform space isomorphism `â„š â‰ƒáµ¤ â„š`, where the LHS has the uniformity from
`HeightOneSpectrum.valuation â„š v` and the RHS has uniformity from
`Rat.padicValuation (natGenerator v)`, for a height-one prime ideal
`v : HeightOneSpectrum (ğ“ â„š)`. -/
noncomputable def valuationEquivPadicValuation (v : HeightOneSpectrum (ğ“ â„š)) :
    WithVal (v.valuation â„š) â‰ƒáµ¤ WithVal (Rat.padicValuation (natGenerator v)) :=
  (valuation_equiv_padicValuation v).uniformEquiv (v.valuation_surjective â„š)
    (surjective_padicValuation _)

/-- The uniform space isomorphism `v.adicCompletion â„š â‰ƒáµ¤ â„š_[natGenerator v]`. -/
noncomputable def adicCompletion.padicUniformEquiv (v : HeightOneSpectrum (ğ“ â„š)) :
    v.adicCompletion â„š â‰ƒáµ¤ â„š_[natGenerator v] :=
  (mapEquiv (valuationEquivPadicValuation v)).trans Padic.withValUniformEquiv

/-- `adicCompletion.padicUniformEquiv` as a `â„š`-algebra isomorphism. -/
noncomputable def adicCompletion.padicAlgEquiv (v : HeightOneSpectrum (ğ“ â„š)) :
    v.adicCompletion â„š â‰ƒâ‚[â„š] â„š_[natGenerator v] where
  __ := (mapRingEquiv _ (valuationEquivPadicValuation v).continuous
      (valuationEquivPadicValuation v).symm.continuous).trans Padic.withValRingEquiv
  commutes' q := by simp

/-- The uniform space isomorphism `v.adicCompletionIntegers â„š â‰ƒáµ¤ â„¤_[natGenerator v]`. -/
noncomputable def adicCompletionIntegers.padicIntUniformEquiv (v : HeightOneSpectrum (ğ“ â„š)) :
    v.adicCompletionIntegers â„š â‰ƒáµ¤ â„¤_[natGenerator v] :=
  let e : v.adicCompletionIntegers â„š â‰ƒáµ¤
      (Valued.v.valuationSubring : ValuationSubring (Rat.padicValuation _).Completion) :=
    (mapEquiv (valuationEquivPadicValuation v)).subtype fun _ â†¦ by
      simpa using (valuation_equiv_padicValuation v).valuedCompletion_le_one_iff
        (v.valuation_surjective â„š) (Rat.surjective_padicValuation _)
  e.trans withValIntegersUniformEquiv

/-- `adicCompletionIntegers.padicIntUniformEquiv` as a ring isomorphism. -/
noncomputable def adicCompletionIntegers.padicIntRingEquiv (v : HeightOneSpectrum (ğ“ â„š)) :
    v.adicCompletionIntegers â„š â‰ƒ+* â„¤_[natGenerator v] :=
  let e : v.adicCompletionIntegers â„š â‰ƒ+*
      (Valued.v.valuationSubring : ValuationSubring (padicValuation _).Completion) :=
    (mapRingEquiv _ (valuationEquivPadicValuation v).continuous
      (valuationEquivPadicValuation v).symm.continuous).restrict _ _ fun _ â†¦ by
      simpa using (valuation_equiv_padicValuation v).valuedCompletion_le_one_iff
        (v.valuation_surjective â„š) (Rat.surjective_padicValuation _)
  e.trans withValIntegersRingEquiv

theorem adicCompletionIntegers.coe_padicIntRingEquiv_apply (v : HeightOneSpectrum (ğ“ â„š))
    (x : v.adicCompletionIntegers â„š) :
    padicIntRingEquiv v x = adicCompletion.padicAlgEquiv v x := rfl

theorem adicCompletion.padicAlgEquiv_bijOn (v : HeightOneSpectrum (ğ“ â„š)) :
    Set.BijOn (padicAlgEquiv v) (v.adicCompletionIntegers â„š) (subring (natGenerator v)) := by
  refine âŸ¨fun x hx â†¦ ?_, (padicAlgEquiv v).injective.injOn, fun y hy â†¦ ?_âŸ©
  Â· rw [â† adicCompletionIntegers.coe_padicIntRingEquiv_apply v âŸ¨x, hxâŸ©]
    exact norm_le_one ((adicCompletionIntegers.padicIntRingEquiv v) âŸ¨x, hxâŸ©)
  Â· obtain âŸ¨x, hxâŸ© := (adicCompletionIntegers.padicIntRingEquiv v).surjective âŸ¨y, hyâŸ©
    refine âŸ¨x, x.2, by rw [â† adicCompletionIntegers.coe_padicIntRingEquiv_apply, hx]âŸ©

end Rat.HeightOneSpectrum
