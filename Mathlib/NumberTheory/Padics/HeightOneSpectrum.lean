/-
Copyright (c) 2025 Salvatore Mercuri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Salvatore Mercuri
-/
module

public import Mathlib.NumberTheory.Padics.WithVal
public import Mathlib.RingTheory.DedekindDomain.AdicValuation
public import Mathlib.RingTheory.Int.Basic
public import Mathlib.Topology.Algebra.Algebra.Equiv

/-!
# Isomorphisms between `adicCompletion ‚Ñö` and `‚Ñö_[p]`

Let `R` have field of fractions `‚Ñö`. If `v : HeightOneSpectrum R`, then `v.adicCompletion ‚Ñö` is
the uniform space completion of `‚Ñö` with respect to the `v`-adic valuation.
On the other hand, `‚Ñö_[p]` is the `p`-adic numbers, defined as the completion of `‚Ñö` with respect
to the `p`-adic norm using the completion of Cauchy sequences. This file constructs continuous
`‚Ñö`-algebra` isomorphisms between the two, as well as continuous `‚Ñ§`-algebra isomorphisms for their
respective rings of integers.

Isomorphisms are provided in both directions, allowing traversal of the following diagram:
```
HeightOneSpectrum R <----------->  Nat.Primes
          |                               |
          |                               |
          v                               v
v.adicCompletionIntegers ‚Ñö  <------->   ‚Ñ§_[p]
          |                               |
          |                               |
          v                               v
v.adicCompletion ‚Ñö  <--------------->   ‚Ñö_[p]
```

## Main definitions
- `Rat.HeightOneSpectrum.primesEquiv` : the equivalence between height-one prime ideals of
  `R` and prime numbers in `‚Ñï`.
- `Rat.HeightOneSpectrum.padicEquiv v` : the continuous `‚Ñö`-algebra isomorphism
  `v.adicCompletion ‚Ñö ‚âÉA[‚Ñö] ‚Ñö_[primesEquiv v]`.
- `Padic.adicCompletionEquiv p` : the continuous `‚Ñö`-algebra isomorphism
  `‚Ñö_[p] ‚âÉA[‚Ñö] (primesEquiv.symm p).adicCompletion ‚Ñö`.
- `Rat.HeightOneSpectrum.adicCompletionIntegers.padicIntEquiv v` : the continuous `‚Ñ§`-algebra
  isomorphism `v.adicCompletionIntegers ‚Ñö ‚âÉA[‚Ñ§] ‚Ñ§_[natGenerator v]`.
- `PadicInt.adicCompletionIntegersEquiv p` : the continuous `‚Ñ§`-algebra isomorphism
  `‚Ñ§_[p] ‚âÉA[‚Ñ§] (primesEquiv.symm p).adicCompletionIntegers ‚Ñö`.

TODO : Abstract the isomorphisms in this file using a universal predicate on adic completions,
along the lines of `IsComplete` + uniformity arises from a valuation + the valuations are
equivalent. It is best to do this after `Valued` has been refactored, or at least after
`adicCompletion` has `IsValuativeTopology` instance.
-/

@[expose] public section

open IsDedekindDomain UniformSpace.Completion NumberField PadicInt

local instance (p : Nat.Primes) : Fact p.1.Prime := ‚ü®p.2‚ü©

variable (R : Type*) [CommRing R] [Algebra R ‚Ñö]

theorem Rat.int_algebraMap_injective : Function.Injective (algebraMap ‚Ñ§ R) :=
  .of_comp (IsScalarTower.algebraMap_eq ‚Ñ§ R ‚Ñö ‚ñ∏ RingHom.injective_int (algebraMap ‚Ñ§ ‚Ñö))

variable [IsIntegralClosure R ‚Ñ§ ‚Ñö]

theorem Rat.int_algebraMap_surjective [IsFractionRing R ‚Ñö] :
    Function.Surjective (algebraMap ‚Ñ§ R) := by
  intro x
  obtain ‚ü®y, hy‚ü© := IsIntegrallyClosed.isIntegral_iff.1 <|
    IsIntegral.algebraMap (B := ‚Ñö) (IsIntegralClosure.isIntegral ‚Ñ§ ‚Ñö x)
  exact ‚ü®y, IsFractionRing.injective R ‚Ñö <| by simp only [‚Üê IsScalarTower.algebraMap_apply, hy]‚ü©

/-- If `R` has field of fractions `‚Ñö` and is the integral closure of `‚Ñ§` in `‚Ñö` then it is
isomorphic to `‚Ñ§`. -/
noncomputable def Rat.IsIntegralClosure.intEquiv : R ‚âÉ+* ‚Ñ§ :=
  (NumberField.RingOfIntegers.equiv R).symm.trans ringOfIntegersEquiv

@[deprecated (since := "2025-12-22")] alias Rat.intEquiv := Rat.IsIntegralClosure.intEquiv

@[simp]
theorem Rat.IsIntegralClosure.intEquiv_apply_eq_ringOfIntegersEquiv (x : ùìû ‚Ñö) :
    intEquiv (ùìû ‚Ñö) x = ringOfIntegersEquiv x := by
  simp [intEquiv, RingOfIntegers.equiv, IsIntegralClosure.equiv, IsIntegralClosure.lift,
    IsIntegralClosure.mk']

namespace Rat.HeightOneSpectrum

variable {R : Type*} [CommRing R] [Algebra R ‚Ñö] [IsIntegralClosure R ‚Ñ§ ‚Ñö]

/-- If `v : HeightOneSpectrum R` then `natGenerator v` is the generator in `‚Ñï` of the corresponding
ideal in `‚Ñ§`. -/
noncomputable def natGenerator (v : HeightOneSpectrum R) : ‚Ñï :=
  Submodule.IsPrincipal.generator (v.asIdeal.map <| IsIntegralClosure.intEquiv R) |>.natAbs

theorem span_natGenerator (v : HeightOneSpectrum R) :
    Ideal.span {(natGenerator v : ‚Ñ§)} = v.asIdeal.map (IsIntegralClosure.intEquiv R) := by
  simp [natGenerator]

theorem natGenerator_dvd_iff (v : HeightOneSpectrum R) {n : ‚Ñï} :
    natGenerator v ‚à£ n ‚Üî ‚Üën ‚àà v.asIdeal.map (IsIntegralClosure.intEquiv R) := by
  rw [‚Üê span_natGenerator, Ideal.mem_span_singleton]
  exact Int.ofNat_dvd.symm

theorem prime_natGenerator (v : HeightOneSpectrum R) : Nat.Prime (natGenerator v) :=
  Int.prime_iff_natAbs_prime.1 <| Submodule.IsPrincipal.prime_generator_of_isPrime _
    ((Ideal.map_eq_bot_iff_of_injective (IsIntegralClosure.intEquiv R).injective).not.2 v.ne_bot)

variable [IsDedekindDomain R] [IsFractionRing R ‚Ñö]

/-- The equivalence between height-one prime ideals of `R` and primes in `‚Ñï`. -/
noncomputable def primesEquiv : HeightOneSpectrum R ‚âÉ Nat.Primes where
  toFun v := ‚ü®natGenerator v, prime_natGenerator v‚ü©
  invFun p :=
    have h : Prime ((Ideal.span {(p.1 : ‚Ñ§)}).map (IsIntegralClosure.intEquiv R).symm) :=
      map_prime_of_equiv _ (by simp [‚Üê Nat.prime_iff_prime_int, p.2]) (by simp [p.2.ne_zero])
    .ofPrime h
  left_inv v := by
    simp only [Ideal.map_symm]
    congr
    rw [‚Üê v.asIdeal.comap_map_of_bijective _ (IsIntegralClosure.intEquiv R).bijective,
      ‚Üê span_natGenerator]
  right_inv p := by
    simp only [Ideal.map_symm, natGenerator, HeightOneSpectrum.ofPrime_asIdeal]
    congr
    simp [Ideal.map_comap_of_surjective _ (IsIntegralClosure.intEquiv R).surjective,
      Int.associated_iff_natAbs.1 (Submodule.IsPrincipal.associated_generator_span_self _)]

theorem valuation_equiv_padicValuation (v : HeightOneSpectrum R) :
    (v.valuation ‚Ñö).IsEquiv (padicValuation (primesEquiv v)) := by
  simp [primesEquiv, Valuation.isEquiv_iff_val_le_one, valuation_le_one_iff_den,
    padicValuation_le_one_iff, natGenerator_dvd_iff,
    map_natCast (IsIntegralClosure.intEquiv R) _ ‚ñ∏ Ideal.apply_mem_of_equiv_iff]

open Valuation

/-- The uniform space isomorphism `‚Ñö ‚âÉ·µ§ ‚Ñö`, where the LHS has the uniformity from
`HeightOneSpectrum.valuation ‚Ñö v` and the RHS has uniformity from
`Rat.padicValuation (natGenerator v)`, for a height-one prime ideal
`v : HeightOneSpectrum R`. -/
noncomputable def withValEquiv (v : HeightOneSpectrum R) :
    WithVal (v.valuation ‚Ñö) ‚âÉ·µ§ WithVal (padicValuation (primesEquiv v)) :=
  (valuation_equiv_padicValuation v).uniformEquiv
    (exists_div_eq_of_surjective (v.valuation_surjective ‚Ñö))
    (exists_div_eq_of_surjective (surjective_padicValuation (primesEquiv v)))

/-- The continuous `‚Ñö`-algebra isomorphism between `v.adicCompletion ‚Ñö` and `‚Ñö_[primesEquiv v]`. -/
noncomputable def adicCompletion.padicEquiv (v : HeightOneSpectrum R) :
    v.adicCompletion ‚Ñö ‚âÉA[‚Ñö] ‚Ñö_[primesEquiv v] where
  __ := (mapRingEquiv _ (withValEquiv v).continuous
      (withValEquiv v).symm.continuous).trans Padic.withValRingEquiv
  __ := ((mapEquiv (withValEquiv v)).trans Padic.withValUniformEquiv).toHomeomorph
  commutes' := by simp

/-- The continuous `‚Ñ§`-algebra isomorphism between `v.adicCompletionIntegers ‚Ñö` and
`‚Ñ§_[primesEquiv v]`. -/
noncomputable def adicCompletionIntegers.padicIntEquiv (v : HeightOneSpectrum R) :
    v.adicCompletionIntegers ‚Ñö ‚âÉA[‚Ñ§] ‚Ñ§_[primesEquiv v] where
  __ := let e := (mapRingEquiv _ (withValEquiv v).continuous
          (withValEquiv v).symm.continuous).restrict _ _ fun _ ‚Ü¶ by
            simpa using (valuation_equiv_padicValuation v).valuedCompletion_le_one_iff
              (v.valuation_surjective ‚Ñö) (surjective_padicValuation _)
        e.trans withValIntegersRingEquiv
  __ := let e := (mapEquiv (withValEquiv v)).subtype fun _ ‚Ü¶ by
          simpa using (valuation_equiv_padicValuation v).valuedCompletion_le_one_iff
            (v.valuation_surjective ‚Ñö) (surjective_padicValuation _)
        (e.trans withValIntegersUniformEquiv).toHomeomorph
  commutes' := by simp

/-- The diagram
```
v.adicCompletionIntegers ‚Ñö  ----->  ‚Ñ§_[primesEquiv v]
      |                               |
      |                               |
      v                               v
v.adicCompletion ‚Ñö  ------------->  ‚Ñö_[primesEquiv v]
```
commutes. -/
theorem adicCompletionIntegers.coe_padicIntEquiv_apply (v : HeightOneSpectrum R)
    (x : v.adicCompletionIntegers ‚Ñö) : padicIntEquiv v x = adicCompletion.padicEquiv v x := rfl

/-- The diagram
```
v.adicCompletionIntegers ‚Ñö  <-----  ‚Ñ§_[primesEquiv v]
      |                               |
      |                               |
      v                               v
v.adicCompletion ‚Ñö  <-------------  ‚Ñö_[primesEquiv v]
```
commutes. -/
theorem adicCompletionIntegers.coe_padicIntEquiv_symm_apply (v : HeightOneSpectrum R)
    (x : ‚Ñ§_[primesEquiv v]) : (adicCompletionIntegers.padicIntEquiv v).symm x =
      (adicCompletion.padicEquiv v).symm x := rfl

theorem adicCompletion.padicEquiv_bijOn (v : HeightOneSpectrum R) :
    Set.BijOn (padicEquiv v) (v.adicCompletionIntegers ‚Ñö) (subring (primesEquiv v)) := by
  refine ‚ü®fun x hx ‚Ü¶ ?_, (padicEquiv v).injective.injOn, fun y hy ‚Ü¶ ?_‚ü©
  ¬∑ rw [‚Üê adicCompletionIntegers.coe_padicIntEquiv_apply v ‚ü®x, hx‚ü©]
    exact norm_le_one ((adicCompletionIntegers.padicIntEquiv v) ‚ü®x, hx‚ü©)
  ¬∑ obtain ‚ü®x, hx‚ü© := (adicCompletionIntegers.padicIntEquiv v).surjective ‚ü®y, hy‚ü©
    refine ‚ü®x, x.2, by rw [‚Üê adicCompletionIntegers.coe_padicIntEquiv_apply, hx]‚ü©

end Rat.HeightOneSpectrum

open Rat.HeightOneSpectrum

namespace Padic

variable (R : Type*) [CommRing R] [IsDedekindDomain R] [Algebra R ‚Ñö] [IsFractionRing R ‚Ñö]
  [IsIntegralClosure R ‚Ñ§ ‚Ñö]

/-- The continuous `‚Ñö`-algebra isomorphism between `‚Ñö_[p]` and
`(primesEquiv.symm p).adicCompletion ‚Ñö`. -/
noncomputable def adicCompletionEquiv (p : Nat.Primes) :
    ‚Ñö_[p] ‚âÉA[‚Ñö] ((primesEquiv (R := R)).symm p).adicCompletion ‚Ñö := by
  apply (ContinuousAlgEquiv.cast (primesEquiv.apply_symm_apply p).symm).trans
    (adicCompletion.padicEquiv (primesEquiv.symm p)).symm

end Padic

namespace PadicInt

open Padic

variable (R : Type*) [CommRing R] [IsDedekindDomain R] [Algebra R ‚Ñö] [IsFractionRing R ‚Ñö]
  [IsIntegralClosure R ‚Ñ§ ‚Ñö]

/-- The continuous `‚Ñ§`-algebra isomorphism between `‚Ñ§_[p]` and
`(primesEquiv.symm p).adicCompletionIntegers ‚Ñö`. -/
noncomputable def adicCompletionIntegersEquiv (p : Nat.Primes) :
    ‚Ñ§_[p] ‚âÉA[‚Ñ§] ((primesEquiv (R := R)).symm p).adicCompletionIntegers ‚Ñö := by
  apply (ContinuousAlgEquiv.cast (primesEquiv.apply_symm_apply p).symm).trans
    (adicCompletionIntegers.padicIntEquiv (primesEquiv.symm p)).symm

/-- The diagram
```
‚Ñ§_[p]  -------->  (primesEquiv.symm p).adicCompletionIntegers ‚Ñö
   |                          |
   |                          |
   v                          v
‚Ñö_[p]  -------->  (primesEquiv.symm p).adicCompletion ‚Ñö
```
commutes. -/
theorem coe_adicCompletionIntegersEquiv_apply (p : Nat.Primes) (x : ‚Ñ§_[p]) :
    (adicCompletionIntegersEquiv R p x) = adicCompletionEquiv R p x := by
  simp only [adicCompletionIntegersEquiv, ContinuousAlgEquiv.trans_apply,
    adicCompletionIntegers.coe_padicIntEquiv_symm_apply,
    adicCompletionEquiv, ContinuousAlgEquiv.trans_apply, ContinuousAlgEquiv.cast_apply,
    EmbeddingLike.apply_eq_iff_eq, Equiv.cast_apply, eq_cast_iff_heq]
  rw [‚Üê Subtype.heq_iff_coe_heq (by rw [primesEquiv.apply_symm_apply])
    (by rw [primesEquiv.apply_symm_apply])]
  exact cast_heq _ _

/-- The diagram
```
‚Ñ§_[p]  <--------  (primesEquiv.symm p).adicCompletionIntegers ‚Ñö
   |                          |
   |                          |
   v                          v
‚Ñö_[p]  <--------  (primesEquiv.symm p).adicCompletion ‚Ñö
```
commutes. -/
theorem coe_adicCompletionIntegersEquiv_symm_apply (p : Nat.Primes)
    (x : (primesEquiv.symm p).adicCompletionIntegers ‚Ñö) :
    (adicCompletionIntegersEquiv R p).symm x = (adicCompletionEquiv R p).symm x := by
  simp only [adicCompletionIntegersEquiv, ContinuousAlgEquiv.symm_trans_apply,
    ContinuousAlgEquiv.symm_symm, adicCompletionEquiv, Equiv.cast_apply, eq_cast_iff_heq,
    ‚Üê adicCompletionIntegers.coe_padicIntEquiv_apply, ContinuousAlgEquiv.cast_symm_apply]
  rw [‚Üê Subtype.heq_iff_coe_heq (by rw [primesEquiv.apply_symm_apply])
    (by rw [primesEquiv.apply_symm_apply])]
  exact cast_heq _ _

end PadicInt
