import Mathlib.NumberTheory.Padics.RingHoms
import Mathlib.Topology.Algebra.Valued.NormedValued
import Mathlib.NumberTheory.Padics.WithVal
import Mathlib.RingTheory.DedekindDomain.AdicValuation
import Mathlib.RingTheory.Int.Basic
import Mathlib.NumberTheory.Padics.ProperSpace

open IsDedekindDomain

open scoped NumberField WithZero

noncomputable
def IsDedekindDomain.HeightOneSpectrum.natGenerator {R : Type*} [CommRing R]
    [h : Nonempty (R â‰ƒ+* â„¤)] (v : HeightOneSpectrum R) : â„• :=
  Submodule.IsPrincipal.generator (v.asIdeal.map h.some) |>.natAbs

namespace IsDedekindDomain.HeightOneSpectrum

noncomputable instance {R : Type*} [CommRing R] [Nonempty (R â‰ƒ+* â„¤)] :
    CoeOut (HeightOneSpectrum R) â„• where
  coe := natGenerator

theorem span_natGenerator {R : Type*} [CommRing R] [h : Nonempty (R â‰ƒ+* â„¤)]
    (v : HeightOneSpectrum R) :
    Ideal.span {â†‘v} = v.asIdeal.map h.some := by
  simp [natGenerator]

theorem natGenerator_dvd_iff {R : Type*} [CommRing R] [h : Nonempty (R â‰ƒ+* â„¤)]
    (v : HeightOneSpectrum R) {n : â„•} :
    â†‘v âˆ£ n â†” â†‘n âˆˆ v.asIdeal.map h.some := by
  rw [â† span_natGenerator, Ideal.mem_span_singleton]
  exact Int.ofNat_dvd.symm

instance {R : Type*} [CommRing R] [h : Nonempty (R â‰ƒ+* â„¤)]
    (v : HeightOneSpectrum R) : Fact (Nat.Prime v) :=
  âŸ¨Int.prime_iff_natAbs_prime.1 <| Submodule.IsPrincipal.prime_generator_of_isPrime _
    ((Ideal.map_eq_bot_iff_of_injective h.some.injective).not.2 v.ne_bot)âŸ©

theorem valuation_equiv_padicValuation {R : Type*} [CommRing R] [IsDedekindDomain R]
    [Algebra R â„š] [IsFractionRing R â„š] [Nonempty (R â‰ƒ+* â„¤)] (ğ”­ : HeightOneSpectrum R) :
    (ğ”­.valuation â„š).IsEquiv (Rat.padicValuation ğ”­) := by
  simp [Valuation.isEquiv_iff_val_le_one, Rat.padicValuation_le_one_iff,
    Rat.valuation_le_one_iff_den, natGenerator_dvd_iff,
    â† Ideal.apply_mem_of_equiv_iff (f := Classical.arbitrary (R â‰ƒ+* â„¤))]

noncomputable def valuationEquivPadicValuation {R : Type*} [CommRing R] [IsDedekindDomain R]
    [Algebra R â„š] [IsFractionRing R â„š] [Nonempty (R â‰ƒ+* â„¤)] (ğ”­ : HeightOneSpectrum R) :
    WithVal (ğ”­.valuation â„š) â‰ƒáµ¤ WithVal (Rat.padicValuation ğ”­) :=
  Valuation.IsEquiv.uniformEquiv (ğ”­.valuation_surjective â„š) (Rat.surjective_padicValuation _)
    ğ”­.valuation_equiv_padicValuation

noncomputable
def adicCompletionEquivPadic {R : Type*} [CommRing R] [IsDedekindDomain R]
    [Algebra R â„š] [IsFractionRing R â„š] [Nonempty (R â‰ƒ+* â„¤)] (ğ”­ : HeightOneSpectrum R) :
    ğ”­.adicCompletion â„š â‰ƒáµ¤ â„š_[ğ”­] :=
  (UniformSpace.Completion.mapEquiv ğ”­.valuationEquivPadicValuation).trans Padic.withValUniformEquiv

open UniformSpace.Completion in
theorem _root_.Valuation.IsEquiv.valuedCompletion_le_one_iff {K : Type*} [Field K] {Î“â‚€ : Type*}
    [LinearOrderedCommGroupWithZero Î“â‚€] {v : Valuation K Î“â‚€} {Î“â‚€' : Type*}
    [LinearOrderedCommGroupWithZero Î“â‚€'] {v' : Valuation K Î“â‚€'} (h : v.IsEquiv v')
    (hv : Function.Surjective v) (hv' : Function.Surjective v') {x : v.Completion} :
    Valued.v x â‰¤ 1 â†” Valued.v (mapEquiv (h.uniformEquiv hv hv') x) â‰¤ 1 := by
  induction x using induction_on with
  | hp =>
    exact (mapEquiv (h.uniformEquiv hv hv')).toHomeomorph.isClosed_setOf_iff
      (Valued.isClopen_closedBall _ one_ne_zero) (Valued.isClopen_closedBall _ one_ne_zero)
  | ih a =>
    rw [Valued.valuedCompletion_apply, â† WithVal.apply_equiv, mapEquiv_coe]
    simpa using h.le_one_iff_le_one

noncomputable def adicCompletionIntegersEquivPadicInt {R : Type*} [CommRing R] [IsDedekindDomain R]
    [Algebra R â„š] [IsFractionRing R â„š] [Nonempty (R â‰ƒ+* â„¤)] (ğ”­ : HeightOneSpectrum R) :
    ğ”­.adicCompletionIntegers â„š â‰ƒáµ¤ â„¤_[ğ”­] :=
  let e : ğ”­.adicCompletionIntegers â„š â‰ƒáµ¤
      (Valued.v.valuationSubring : ValuationSubring (Rat.padicValuation ğ”­).Completion) :=
    (UniformSpace.Completion.mapEquiv ğ”­.valuationEquivPadicValuation).subtype fun _ â†¦ by
      simpa using ğ”­.valuation_equiv_padicValuation.valuedCompletion_le_one_iff
        (ğ”­.valuation_surjective â„š) (Rat.surjective_padicValuation _)
  e.trans PadicInt.withValIntegersUniformEquiv

instance : Nonempty (ğ“ â„š â‰ƒ+* â„¤) := âŸ¨Rat.ringOfIntegersEquivâŸ©

instance {Î“â‚€ : Type*} [LinearOrderedCommGroupWithZero Î“â‚€] {v : Valuation â„š Î“â‚€} :
    Nonempty (ğ“ (WithVal v) â‰ƒ+* â„¤) := âŸ¨Rat.ringOfIntegersWithValEquiv vâŸ©

noncomputable example (ğ”­ : HeightOneSpectrum (ğ“ â„š)) : ğ”­.adicCompletion â„š â‰ƒáµ¤ â„š_[ğ”­] :=
  ğ”­.adicCompletionEquivPadic

noncomputable example (ğ”­ : HeightOneSpectrum (ğ“ â„š)) : CompactSpace (ğ”­.adicCompletionIntegers â„š) :=
  ğ”­.adicCompletionIntegersEquivPadicInt.toHomeomorph.symm.compactSpace

end IsDedekindDomain.HeightOneSpectrum
