/-
Copyright (c) 2025 Salvatore Mercuri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Salvatore Mercuri
-/
module

public import Mathlib.NumberTheory.Padics.WithVal
public import Mathlib.RingTheory.DedekindDomain.AdicValuation
public import Mathlib.RingTheory.Int.Basic
public import Mathlib.Topology.Algebra.Algebra.Equiv

/-!
# Isomorphisms between `adicCompletion ‚Ñö` and `‚Ñö_[p]`

If `v : HeightOneSpectrum (ùìû ‚Ñö)`, then `v.adicCompletion ‚Ñö` is the uniform space completion of `‚Ñö`
with respect to the `v`-adic valuation, which is defined more generally via Dedekind domains and
their fields of fractions. On the other hand, `‚Ñö_[p]` is the `p`-adic numbers, defined as the
completion of `‚Ñö` with respect to the `p`-adic norm using the completion of Cauchy sequences.
This file constructs continuous `‚Ñö`-algebra` isomorphisms between the two, as well as continuous
`‚Ñ§`-algebra isomorphisms for their respective rings of integers.

Isomorphisms are provided in both directions, allowing traversal of the following diagram:
```
HeightOneSpectrum (ùìû ‚Ñö) <----------->  Nat.Primes
          |                               |
          |                               |
          v                               v
v.adicCompletionIntegers ‚Ñö  <------->   ‚Ñ§_[p]
          |                               |
          |                               |
          v                               v
v.adicCompletion ‚Ñö  <--------------->   ‚Ñö_[p]
```

## Main definitions
- `Rat.HeightOneSpectrum.primesEquiv` : the equivalence between height-one prime ideals of
  `ùìû ‚Ñö` and prime numbers in `‚Ñï`.
- `Rat.HeightOneSpectrum.padicEquiv v` : the continuous `‚Ñö`-algebra isomorphism
  `v.adicCompletion ‚Ñö ‚âÉA[‚Ñö] ‚Ñö_[primesEquiv v]`.
- `Padic.adicCompletionEquiv p` : the continuous `‚Ñö`-algebra isomorphism
  `‚Ñö_[p] ‚âÉA[‚Ñö] (primesEquiv.symm p).adicCompletion ‚Ñö`.
- `Rat.HeightOneSpectrum.adicCompletionIntegers.padicIntEquiv v` : the continuous `‚Ñ§`-algebra
  isomorphism `v.adicCompletionIntegers ‚Ñö ‚âÉA[‚Ñ§] ‚Ñ§_[natGenerator v]`.
- `PadicInt.adicCompletionIntegersEquiv p` : the continuous `‚Ñ§`-algebra isomorphism
  `‚Ñ§_[p] ‚âÉA[‚Ñ§] (primesEquiv.symm p).adicCompletionIntegers ‚Ñö`.
-/

@[expose] public section

open IsDedekindDomain UniformSpace.Completion NumberField PadicInt

local instance (p : Nat.Primes) : Fact p.1.Prime := ‚ü®p.2‚ü©

namespace Rat.HeightOneSpectrum

/-- The generator in `‚Ñï` of a height-one prime ideal in `ùìû ‚Ñö`. -/
noncomputable def natGenerator (v : HeightOneSpectrum (ùìû ‚Ñö)) : ‚Ñï :=
  Submodule.IsPrincipal.generator (v.asIdeal.map ringOfIntegersEquiv) |>.natAbs

theorem span_natGenerator (v : HeightOneSpectrum (ùìû ‚Ñö)) :
    Ideal.span {(natGenerator v : ‚Ñ§)} = v.asIdeal.map ringOfIntegersEquiv := by
  simp [natGenerator]

theorem natGenerator_dvd_iff (v : HeightOneSpectrum (ùìû ‚Ñö)) {n : ‚Ñï} :
    natGenerator v ‚à£ n ‚Üî ‚Üën ‚àà v.asIdeal.map ringOfIntegersEquiv := by
  rw [‚Üê span_natGenerator, Ideal.mem_span_singleton]
  exact Int.ofNat_dvd.symm

theorem prime_natGenerator (v : HeightOneSpectrum (ùìû ‚Ñö)) : Nat.Prime (natGenerator v) :=
  Int.prime_iff_natAbs_prime.1 <| Submodule.IsPrincipal.prime_generator_of_isPrime _
    ((Ideal.map_eq_bot_iff_of_injective ringOfIntegersEquiv.injective).not.2 v.ne_bot)

local instance (p : Nat.Primes) : (Ideal.span {(p.1 : ‚Ñ§)}).IsPrime :=
  (Ideal.span_singleton_prime (by simp [p.2.ne_zero])).2 <| Nat.prime_iff_prime_int.1 p.2

/-- The equivalence between height-one prime ideals of `ùìû ‚Ñö` and primes in `‚Ñï`. -/
noncomputable def primesEquiv : HeightOneSpectrum (ùìû ‚Ñö) ‚âÉ Nat.Primes where
  toFun v := ‚ü®natGenerator v, prime_natGenerator v‚ü©
  invFun p :=
    have h : Prime ((Ideal.span {(p.1 : ‚Ñ§)}).map ringOfIntegersEquiv.symm) :=
      map_prime_of_equiv _ (by simp [‚Üê Nat.prime_iff_prime_int, p.2]) (by simp [p.2.ne_zero])
    .ofPrime h
  left_inv v := by
    simp only [Ideal.map_symm]
    congr
    rw [‚Üê v.asIdeal.comap_map_of_bijective _ ringOfIntegersEquiv.bijective, ‚Üê span_natGenerator]
  right_inv p := by
    simp only [Ideal.map_symm, natGenerator, HeightOneSpectrum.ofPrime_asIdeal]
    congr
    simp [Ideal.map_comap_of_surjective _ ringOfIntegersEquiv.surjective,
      Int.associated_iff_natAbs.1 (Submodule.IsPrincipal.associated_generator_span_self _)]

theorem valuation_equiv_padicValuation (v : HeightOneSpectrum (ùìû ‚Ñö)) :
    (v.valuation ‚Ñö).IsEquiv (padicValuation (primesEquiv v)) := by
  simp [Valuation.isEquiv_iff_val_le_one, padicValuation_le_one_iff, valuation_le_one_iff_den,
    primesEquiv, natGenerator_dvd_iff, ‚Üê Ideal.apply_mem_of_equiv_iff (f := ringOfIntegersEquiv)]

open Valuation

/-- The uniform space isomorphism `‚Ñö ‚âÉ·µ§ ‚Ñö`, where the LHS has the uniformity from
`HeightOneSpectrum.valuation ‚Ñö v` and the RHS has uniformity from
`Rat.padicValuation (natGenerator v)`, for a height-one prime ideal
`v : HeightOneSpectrum (ùìû ‚Ñö)`. -/
noncomputable def withValEquiv (v : HeightOneSpectrum (ùìû ‚Ñö)) :
    WithVal (v.valuation ‚Ñö) ‚âÉ·µ§ WithVal (padicValuation (primesEquiv v)) :=
  (valuation_equiv_padicValuation v).uniformEquiv
    (fun Œ≥ ‚Ü¶ by obtain ‚ü®r, hr‚ü© := v.valuation_surjective ‚Ñö Œ≥; exact ‚ü®r, 1, by aesop‚ü©)
    (fun Œ≥ ‚Ü¶ by obtain ‚ü®r, hr‚ü© := surjective_padicValuation (primesEquiv v) Œ≥
                exact ‚ü®r, 1, by aesop‚ü©)

/-- The continuous `‚Ñö`-algebra isomorphism between `v.adicCompletion ‚Ñö` and `‚Ñö_[primesEquiv v]`. -/
noncomputable def adicCompletion.padicEquiv (v : HeightOneSpectrum (ùìû ‚Ñö)) :
    v.adicCompletion ‚Ñö ‚âÉA[‚Ñö] ‚Ñö_[primesEquiv v] where
  __ := (mapRingEquiv _ (withValEquiv v).continuous
      (withValEquiv v).symm.continuous).trans Padic.withValRingEquiv
  __ := ((mapEquiv (withValEquiv v)).trans Padic.withValUniformEquiv).toHomeomorph
  commutes' := by simp

/-- The continuous `‚Ñ§`-algebra isomorphism between `v.adicCompletionIntegers ‚Ñö` and
`‚Ñ§_[primesEquiv v]`. -/
noncomputable def adicCompletionIntegers.padicIntEquiv (v : HeightOneSpectrum (ùìû ‚Ñö)) :
    v.adicCompletionIntegers ‚Ñö ‚âÉA[‚Ñ§] ‚Ñ§_[primesEquiv v] where
  __ := let e := (mapRingEquiv _ (withValEquiv v).continuous
          (withValEquiv v).symm.continuous).restrict _ _ fun _ ‚Ü¶ by
            simpa using (valuation_equiv_padicValuation v).valuedCompletion_le_one_iff
              (v.valuation_surjective ‚Ñö) (surjective_padicValuation _)
        e.trans withValIntegersRingEquiv
  __ := let e := (mapEquiv (withValEquiv v)).subtype fun _ ‚Ü¶ by
          simpa using (valuation_equiv_padicValuation v).valuedCompletion_le_one_iff
            (v.valuation_surjective ‚Ñö) (surjective_padicValuation _)
        (e.trans withValIntegersUniformEquiv).toHomeomorph
  commutes' := by simp

/-- The diagram
```
v.adicCompletionIntegers ‚Ñö  ----->  ‚Ñ§_[primesEquiv v]
      |                               |
      |                               |
      v                               v
v.adicCompletion ‚Ñö  ------------->  ‚Ñö_[primesEquiv v]
```
commutes. -/
theorem adicCompletionIntegers.coe_padicIntEquiv_apply (v : HeightOneSpectrum (ùìû ‚Ñö))
    (x : v.adicCompletionIntegers ‚Ñö) : padicIntEquiv v x = adicCompletion.padicEquiv v x := rfl

/-- The diagram
```
v.adicCompletionIntegers ‚Ñö  <-----  ‚Ñ§_[primesEquiv v]
      |                               |
      |                               |
      v                               v
v.adicCompletion ‚Ñö  <-------------  ‚Ñö_[primesEquiv v]
```
commutes. -/
theorem adicCompletionIntegers.coe_padicIntEquiv_symm_apply (v : HeightOneSpectrum (ùìû ‚Ñö))
    (x : ‚Ñ§_[primesEquiv v]) : (adicCompletionIntegers.padicIntEquiv v).symm x =
      (adicCompletion.padicEquiv v).symm x := rfl

theorem adicCompletion.padicEquiv_bijOn (v : HeightOneSpectrum (ùìû ‚Ñö)) :
    Set.BijOn (padicEquiv v) (v.adicCompletionIntegers ‚Ñö) (subring (primesEquiv v)) := by
  refine ‚ü®fun x hx ‚Ü¶ ?_, (padicEquiv v).injective.injOn, fun y hy ‚Ü¶ ?_‚ü©
  ¬∑ rw [‚Üê adicCompletionIntegers.coe_padicIntEquiv_apply v ‚ü®x, hx‚ü©]
    exact norm_le_one ((adicCompletionIntegers.padicIntEquiv v) ‚ü®x, hx‚ü©)
  ¬∑ obtain ‚ü®x, hx‚ü© := (adicCompletionIntegers.padicIntEquiv v).surjective ‚ü®y, hy‚ü©
    refine ‚ü®x, x.2, by rw [‚Üê adicCompletionIntegers.coe_padicIntEquiv_apply, hx]‚ü©

end Rat.HeightOneSpectrum

open Rat.HeightOneSpectrum

namespace Padic

/-- The continuous `‚Ñö`-algebra isomorphism between `‚Ñö_[p]` and
`(primesEquiv.symm p).adicCompletion ‚Ñö`. -/
noncomputable def adicCompletionEquiv (p : Nat.Primes) :
    ‚Ñö_[p] ‚âÉA[‚Ñö] (primesEquiv.symm p).adicCompletion ‚Ñö := by
  apply (ContinuousAlgEquiv.cast (primesEquiv.apply_symm_apply p).symm).trans
    (adicCompletion.padicEquiv (primesEquiv.symm p)).symm

end Padic

namespace PadicInt

open Padic

/-- The continuous `‚Ñ§`-algebra isomorphism between `‚Ñ§_[p]` and
`(primesEquiv.symm p).adicCompletionIntegers ‚Ñö`. -/
noncomputable def adicCompletionIntegersEquiv (p : Nat.Primes) :
    ‚Ñ§_[p] ‚âÉA[‚Ñ§] (primesEquiv.symm p).adicCompletionIntegers ‚Ñö := by
  apply (ContinuousAlgEquiv.cast (primesEquiv.apply_symm_apply p).symm).trans
    (adicCompletionIntegers.padicIntEquiv (primesEquiv.symm p)).symm

/-- The diagram
```
‚Ñ§_[p]  -------->  (primesEquiv.symm p).adicCompletionIntegers ‚Ñö
   |                          |
   |                          |
   v                          v
‚Ñö_[p]  -------->  (primesEquiv.symm p).adicCompletion ‚Ñö
```
commutes. -/
theorem coe_adicCompletionIntegersEquiv_apply (p : Nat.Primes) (x : ‚Ñ§_[p]) :
    (adicCompletionIntegersEquiv p x) = adicCompletionEquiv p x := by
  simp only [adicCompletionIntegersEquiv, ContinuousAlgEquiv.trans_apply,
    adicCompletionIntegers.coe_padicIntEquiv_symm_apply,
    adicCompletionEquiv, ContinuousAlgEquiv.trans_apply, ContinuousAlgEquiv.cast_apply,
    EmbeddingLike.apply_eq_iff_eq, Equiv.cast_apply, eq_cast_iff_heq]
  rw [‚Üê Subtype.heq_iff_coe_heq (by rw [primesEquiv.apply_symm_apply])
    (by rw [primesEquiv.apply_symm_apply])]
  exact cast_heq _ _

/-- The diagram
```
‚Ñ§_[p]  <--------  (primesEquiv.symm p).adicCompletionIntegers ‚Ñö
   |                          |
   |                          |
   v                          v
‚Ñö_[p]  <--------  (primesEquiv.symm p).adicCompletion ‚Ñö
```
commutes. -/
theorem coe_adicCompletionIntegersEquiv_symm_apply (p : Nat.Primes)
    (x : (primesEquiv.symm p).adicCompletionIntegers ‚Ñö) :
    (adicCompletionIntegersEquiv p).symm x = (adicCompletionEquiv p).symm x := by
  simp only [adicCompletionIntegersEquiv, ContinuousAlgEquiv.symm_trans_apply,
    ContinuousAlgEquiv.symm_symm, adicCompletionEquiv, Equiv.cast_apply, eq_cast_iff_heq,
    ‚Üê adicCompletionIntegers.coe_padicIntEquiv_apply, ContinuousAlgEquiv.cast_symm_apply]
  rw [‚Üê Subtype.heq_iff_coe_heq (by rw [primesEquiv.apply_symm_apply])
    (by rw [primesEquiv.apply_symm_apply])]
  exact cast_heq _ _

end PadicInt
