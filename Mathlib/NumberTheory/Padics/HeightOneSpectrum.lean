import Mathlib.NumberTheory.Padics.RingHoms
import Mathlib.Topology.Algebra.Valued.NormedValued
import Mathlib.NumberTheory.Padics.WithVal
import Mathlib.RingTheory.DedekindDomain.AdicValuation
import Mathlib.RingTheory.Int.Basic

open IsDedekindDomain

open scoped NumberField WithZero

noncomputable def UniformSpace.Completion.mapEquiv {Î± Î² : Type*} [UniformSpace Î±] [UniformSpace Î²]
    (h : Î± â‰ƒáµ¤ Î²) : UniformSpace.Completion Î± â‰ƒáµ¤ UniformSpace.Completion Î² where
  toFun := .map h
  invFun := .map h.symm
  uniformContinuous_toFun := uniformContinuous_map
  uniformContinuous_invFun := uniformContinuous_map
  left_inv := by
    rw [Function.leftInverse_iff_comp]
    apply ext (.comp continuous_map continuous_map) continuous_id fun a â†¦ ?_
    simp [map_coe h.uniformContinuous, map_coe h.symm.uniformContinuous]
  right_inv := by
    rw [Function.rightInverse_iff_comp]
    apply ext (.comp continuous_map continuous_map) continuous_id fun a â†¦ ?_
    simp [map_coe h.symm.uniformContinuous, map_coe h.uniformContinuous]

def HeightOneSpectrum.mapEquiv {R S F : Type*} [CommRing R] [CommRing S] [EquivLike F R S]
    [RingEquivClass F R S] (f : F) : HeightOneSpectrum R â‰ƒ HeightOneSpectrum S where
  toFun v := âŸ¨v.asIdeal.map f, Ideal.map_isPrime_of_equiv f,
    mt (Ideal.map_eq_bot_iff_of_injective (EquivLike.injective f)).1 v.ne_botâŸ©
  invFun v := âŸ¨v.asIdeal.map (RingEquiv.symm f), Ideal.map_isPrime_of_equiv _,
    mt (Ideal.map_eq_bot_iff_of_injective (EquivLike.injective _)).1 v.ne_botâŸ©
  left_inv v := by
    simp only [Ideal.map_symm]
    congr
    exact Ideal.comap_map_of_bijective f (EquivLike.toEquiv f).bijective
  right_inv v := by
    simp only [Ideal.map_symm]
    congr
    exact Ideal.map_comap_of_surjective f (EquivLike.toEquiv f).surjective _

noncomputable
def Rat.ringOfIntegersSpectrumEquiv : HeightOneSpectrum (ğ“ â„š) â‰ƒ HeightOneSpectrum â„¤ :=
    HeightOneSpectrum.mapEquiv ringOfIntegersEquiv

noncomputable
def IsDedekindDomain.HeightOneSpectrum.toNatGenerator {R : Type*} [CommRing R]
    [h : Nonempty (R â‰ƒ+* â„¤)] (v : HeightOneSpectrum R) : â„• :=
  Submodule.IsPrincipal.generator (v.asIdeal.map h.some) |>.natAbs

namespace IsDedekindDomain.HeightOneSpectrum

theorem toNatGenerator_span {R : Type*} [CommRing R] [h : Nonempty (R â‰ƒ+* â„¤)]
    (v : HeightOneSpectrum R) :
    v.asIdeal.map h.some = Ideal.span {â†‘v.toNatGenerator} := by
  simp [toNatGenerator]

theorem toNatGenerator_dvd_iff {R : Type*} [CommRing R] [h : Nonempty (R â‰ƒ+* â„¤)]
    (v : HeightOneSpectrum R) {n : â„•} :
    v.toNatGenerator âˆ£ n â†” â†‘n âˆˆ v.asIdeal.map h.some := by
  rw [toNatGenerator_span, Ideal.mem_span_singleton]
  exact Int.ofNat_dvd.symm

instance {R : Type*} [CommRing R] [Nonempty (R â‰ƒ+* â„¤)]
    (v : HeightOneSpectrum R) : Fact (v.toNatGenerator).Prime :=
  let f := Classical.arbitrary (R â‰ƒ+* â„¤)
  âŸ¨Int.prime_iff_natAbs_prime.1 <| Submodule.IsPrincipal.prime_generator_of_isPrime _
    ((Ideal.map_eq_bot_iff_of_injective f.injective).not.2 v.ne_bot)âŸ©

noncomputable def toRatpadicValuation {R : Type*} [CommRing R] [Nonempty (R â‰ƒ+* â„¤)]
    (v : HeightOneSpectrum R) : Valuation â„š â„¤áµâ° :=
  Rat.padicValuation v.toNatGenerator

theorem valuation_equiv_toRatpadicValuation {R : Type*} [CommRing R] [IsDedekindDomain R]
    [Algebra R â„š] [IsFractionRing R â„š] [h : Nonempty (R â‰ƒ+* â„¤)] (ğ”­ : HeightOneSpectrum R) :
    (ğ”­.valuation â„š).IsEquiv (ğ”­.toRatpadicValuation) := by
  simp [Valuation.isEquiv_iff_val_le_one, Rat.padicValuation_le_one_iff,
    Rat.valuation_le_one_iff_den, toNatGenerator_dvd_iff, toRatpadicValuation,
    â† Ideal.apply_mem_of_equiv_iff (f := h.some)]

noncomputable def withValEquiv {R : Type*} [CommRing R] [IsDedekindDomain R]
    [Algebra R â„š] [IsFractionRing R â„š] [Nonempty (R â‰ƒ+* â„¤)] (ğ”­ : HeightOneSpectrum R) :
    WithVal (ğ”­.valuation â„š) â‰ƒáµ¤ WithVal ğ”­.toRatpadicValuation :=
  Valuation.IsEquiv.uniformEquiv (ğ”­.valuation_surjective â„š) (Rat.surjective_padicValuation _)
    ğ”­.valuation_equiv_toRatpadicValuation

noncomputable
def _root_.Rat.adicCompletionEquivPadicCompletion {R : Type*} [CommRing R] [IsDedekindDomain R]
    [Algebra R â„š] [IsFractionRing R â„š] [Nonempty (R â‰ƒ+* â„¤)] (ğ”­ : HeightOneSpectrum R) :
    ğ”­.adicCompletion â„š â‰ƒáµ¤ â„š_[ğ”­.toNatGenerator] := by
  apply UniformSpace.Completion.mapEquiv ğ”­.withValEquiv |>.trans Padic.withValUniformEquiv

open UniformSpace.Completion in
theorem _root_.Valuation.IsEquiv.valuedCompletion_le_one_iff {K : Type*} [Field K] {Î“â‚€ : Type*}
    [LinearOrderedCommGroupWithZero Î“â‚€] {v : Valuation K Î“â‚€} {Î“â‚€' : Type*}
    [LinearOrderedCommGroupWithZero Î“â‚€'] {v' : Valuation K Î“â‚€'} (h : v.IsEquiv v')
    (hv : Function.Surjective v) (hv' : Function.Surjective v')
    {x : v.Completion} :
    Valued.v x â‰¤ 1 â†” Valued.v (mapEquiv (h.uniformEquiv hv hv') x) â‰¤ 1 := by
  apply UniformSpace.Completion.induction_on
    (p := fun x â†¦ Valued.v x â‰¤ 1 â†” Valued.v (mapEquiv (h.uniformEquiv hv hv') x) â‰¤ 1) x
  Â· have : â‡‘(mapEquiv (h.uniformEquiv hv hv')) =
        â‡‘(mapEquiv (h.uniformEquiv hv hv')).toHomeomorph := rfl
    simp_rw [this]
    apply Homeomorph.isClosed_setOf_iff (q := fun x â†¦ Valued.v x â‰¤ 1)
      (hs := Valued.isClopen_closedBall _ (by norm_num))
      (ht := Valued.isClopen_closedBall _ (by norm_num))
  Â· intro a
    simp [â† WithVal.apply_equiv, mapEquiv]
    rw [UniformSpace.Completion.map_coe
      (Valuation.IsEquiv.uniformEquiv hv hv' h).uniformContinuous]
    simp [Valuation.IsEquiv.uniformEquiv, Equiv.toUniformEquivOfIsUniformInducing]
    exact h.le_one_iff_le_one (x := WithVal.equiv v a)

noncomputable
def _root_.Rat.adicCompletionIntegersEquivPadicInt {R : Type*} [CommRing R] [IsDedekindDomain R]
    [Algebra R â„š] [IsFractionRing R â„š] [Nonempty (R â‰ƒ+* â„¤)] (ğ”­ : HeightOneSpectrum R) :
    ğ”­.adicCompletionIntegers â„š â‰ƒáµ¤
      (Valued.v : Valuation ğ”­.toRatpadicValuation.Completion _).valuationSubring := by
  apply (UniformSpace.Completion.mapEquiv ğ”­.withValEquiv).subtype
  intro
  simpa using ğ”­.valuation_equiv_toRatpadicValuation.valuedCompletion_le_one_iff
    (ğ”­.valuation_surjective â„š) (Rat.surjective_padicValuation _)

noncomputable
def _root_.Rat.adicCompletionIntegersEquivPadicIntRingEquiv {R : Type*}
    [CommRing R] [IsDedekindDomain R] [Algebra R â„š] [IsFractionRing R â„š]
    [Nonempty (R â‰ƒ+* â„¤)] (ğ”­ : HeightOneSpectrum R) :
    ğ”­.adicCompletionIntegers â„š â‰ƒáµ¤ â„¤_[ğ”­.toNatGenerator] :=
  (Rat.adicCompletionIntegersEquivPadicInt ğ”­).trans PadicInt.withValIntegersUniformEquiv

instance : Nonempty (ğ“ â„š â‰ƒ+* â„¤) := âŸ¨Rat.ringOfIntegersEquivâŸ©

instance {Î“â‚€ : Type*} [LinearOrderedCommGroupWithZero Î“â‚€] {v : Valuation â„š Î“â‚€} :
    Nonempty (ğ“ (WithVal v) â‰ƒ+* â„¤) := âŸ¨Rat.ringOfIntegersWithValEquiv vâŸ©

noncomputable
example (ğ”­ : HeightOneSpectrum (ğ“ â„š)) : ğ”­.adicCompletion â„š â‰ƒáµ¤ â„š_[ğ”­.toNatGenerator] :=
  Rat.adicCompletionEquivPadicCompletion ğ”­

end IsDedekindDomain.HeightOneSpectrum
