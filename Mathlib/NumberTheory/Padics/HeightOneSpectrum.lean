/-
Copyright (c) 2025 Salvatore Mercuri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Salvatore Mercuri
-/
module

public import Mathlib.NumberTheory.Padics.WithVal
public import Mathlib.RingTheory.DedekindDomain.AdicValuation
public import Mathlib.RingTheory.Int.Basic

/-!
# Isomorphisms between `adicCompletion â„š` and `â„š_[p]`

If `v : HeightOneSpectrum â„š`, then `v.adicCompletion â„š` is the uniform space completion of `â„š`
with respect to the `v`-adic valuation, which definition generalises to Dedekind domains and
their field of fractions. On the other hand, `â„š_[p]` is the `p`-adic numbers, defined as the
completion of `â„š` with respect to the `p`-adic norm using the completion of Cauchy sequences.
This file constructs uniform and `â„š`-algebra` isomorphisms between the two, as well as for their
respective rings of integers.

Isomorphisms are provided in both directions, allowing traversal of the following diagram:
```
v.adicCompletion â„š  <------------->  â„š_[p]
      â†‘                               â†‘
      |                               |
      |                               |
v.adicCompletionIntegers â„š  <----->  â„¤_[p]
      â†‘                               â†‘
      |                               |
      |                               |
HeightOneSpectrum (ğ“ â„š) <-------> Nat.Primes
```

## Main definitions
- `Rat.HeightOneSpectrum.primesEquiv` : the equivalence between height-one prime ideals of
  `ğ“ â„š` and prime numbers in `â„•`.
- `Rat.HeightOneSpectrum.padicUniformEquiv v` : `v.adicCompletion â„š â‰ƒáµ¤ â„š_[primesEquiv v]`.
- `Padic.adicCompletionUniformEquiv p` : `â„š_[p] â‰ƒáµ¤ (primesEquiv.symm âŸ¨p, _âŸ©).adicCompletion â„š`.
- `Rat.HeightOneSpectrum.padicAlgEquiv v` : `v.adicCompletion â„š â‰ƒâ‚[â„š] â„š_[primesEquiv v]`.
- `Padic.adicCompletionAlgEquiv p` : `â„š_[p] â‰ƒâ‚[â„š] (primesEquiv.symm âŸ¨p, _âŸ©).adicCompletion â„š`.
- `Rat.HeightOneSpectrum.adicCompletionIntegers.padicIntUniformEquiv v` :
  `v.adicCompletionIntegers â„š â‰ƒáµ¤ â„¤_[natGenerator v]`.
- `PadicInt.adicCompletionIntegersUniformEquiv p` :
  `â„¤_[p] â‰ƒáµ¤ (primesEquiv.symm âŸ¨p, _âŸ©).adicCompletionIntegers â„š`.
- `Rat.HeightOneSpectrum.adicCompletionIntegers.padicIntRingEquiv v` :
  `v.adicCompletionIntegers â„š â‰ƒ+* â„¤_[natGenerator v]`.
- `PadicInt.adicCompletionIntegersRingEqui p` :
  `â„¤_[p] â‰ƒâ‚[â„¤] (primesEquiv.symm âŸ¨p, _âŸ©).adicCompletionIntegers â„š`.
-/

@[expose] public section

open IsDedekindDomain UniformSpace.Completion NumberField PadicInt

namespace Rat.HeightOneSpectrum

/-- The generator in `â„•` of a height-one prime ideal in `ğ“ â„š`. -/
noncomputable def natGenerator (v : HeightOneSpectrum (ğ“ â„š)) : â„• :=
  Submodule.IsPrincipal.generator (v.asIdeal.map ringOfIntegersEquiv) |>.natAbs

theorem span_natGenerator (v : HeightOneSpectrum (ğ“ â„š)) :
    Ideal.span {(natGenerator v : â„¤)} = v.asIdeal.map ringOfIntegersEquiv := by
  simp [natGenerator]

theorem natGenerator_dvd_iff (v : HeightOneSpectrum (ğ“ â„š)) {n : â„•} :
    natGenerator v âˆ£ n â†” â†‘n âˆˆ v.asIdeal.map ringOfIntegersEquiv := by
  rw [â† span_natGenerator, Ideal.mem_span_singleton]
  exact Int.ofNat_dvd.symm

theorem prime_natGenerator (v : HeightOneSpectrum (ğ“ â„š)) : Nat.Prime (natGenerator v) :=
  Int.prime_iff_natAbs_prime.1 <| Submodule.IsPrincipal.prime_generator_of_isPrime _
    ((Ideal.map_eq_bot_iff_of_injective ringOfIntegersEquiv.injective).not.2 v.ne_bot)

local instance (p : Nat.Primes) : (Ideal.span {(p.1 : â„¤)}).IsPrime :=
  (Ideal.span_singleton_prime (by simp [p.2.ne_zero])).2 <| Nat.prime_iff_prime_int.1 p.2

@[simps]
noncomputable def primesEquiv : HeightOneSpectrum (ğ“ â„š) â‰ƒ Nat.Primes where
  toFun v := âŸ¨natGenerator v, prime_natGenerator vâŸ©
  invFun p :=
    have h : Prime ((Ideal.span {(p.1 : â„¤)}).map ringOfIntegersEquiv.symm) :=
      map_prime_of_equiv _ (by simp [â† Nat.prime_iff_prime_int, p.2]) (by simp [p.2.ne_zero])
    .ofPrime h
  left_inv v := by
    simp only [Ideal.map_symm]
    congr
    rw [â† v.asIdeal.comap_map_of_bijective _ ringOfIntegersEquiv.bijective, â† span_natGenerator]
  right_inv p := by
    simp only [Ideal.map_symm, natGenerator, HeightOneSpectrum.ofPrime_asIdeal]
    congr
    simp [Ideal.map_comap_of_surjective _ ringOfIntegersEquiv.surjective,
      Int.associated_iff_natAbs.1 (Submodule.IsPrincipal.associated_generator_span_self _)]

local instance (p : Nat.Primes) : Fact p.1.Prime := âŸ¨p.2âŸ©

theorem valuation_equiv_padicValuation (v : HeightOneSpectrum (ğ“ â„š)) :
    (v.valuation â„š).IsEquiv (padicValuation (primesEquiv v)) := by
  simp [Valuation.isEquiv_iff_val_le_one, padicValuation_le_one_iff, valuation_le_one_iff_den,
    natGenerator_dvd_iff, â† Ideal.apply_mem_of_equiv_iff (f := ringOfIntegersEquiv)]

open Valuation

/-- The uniform space isomorphism `â„š â‰ƒáµ¤ â„š`, where the LHS has the uniformity from
`HeightOneSpectrum.valuation â„š v` and the RHS has uniformity from
`Rat.padicValuation (natGenerator v)`, for a height-one prime ideal
`v : HeightOneSpectrum (ğ“ â„š)`. -/
noncomputable def valuationEquivPadicValuation (v : HeightOneSpectrum (ğ“ â„š)) :
    WithVal (v.valuation â„š) â‰ƒáµ¤ WithVal (padicValuation (primesEquiv v)) :=
  (valuation_equiv_padicValuation v).uniformEquiv
    (fun Î³ â†¦ by obtain âŸ¨r, hrâŸ© := v.valuation_surjective â„š Î³; exact âŸ¨r, 1, by aesopâŸ©)
    (fun Î³ â†¦ by
      obtain âŸ¨r, hrâŸ© := surjective_padicValuation (primesEquiv v) Î³;
      exact âŸ¨r, 1, by aesopâŸ©)

/-- The uniform space isomorphism `v.adicCompletion â„š â‰ƒáµ¤ â„š_[natGenerator v]`. -/
noncomputable def adicCompletion.padicUniformEquiv (v : HeightOneSpectrum (ğ“ â„š)) :
    v.adicCompletion â„š â‰ƒáµ¤ â„š_[primesEquiv v] :=
  (mapEquiv (valuationEquivPadicValuation v)).trans Padic.withValUniformEquiv

/-- `adicCompletion.padicUniformEquiv` as a `â„š`-algebra isomorphism. -/
noncomputable def adicCompletion.padicAlgEquiv (v : HeightOneSpectrum (ğ“ â„š)) :
    v.adicCompletion â„š â‰ƒâ‚[â„š] â„š_[primesEquiv v] where
  __ := (mapRingEquiv _ (valuationEquivPadicValuation v).continuous
      (valuationEquivPadicValuation v).symm.continuous).trans Padic.withValRingEquiv
  commutes' q := by simp

/-- The uniform space isomorphism `v.adicCompletionIntegers â„š â‰ƒáµ¤ â„¤_[natGenerator v]`. -/
noncomputable def adicCompletionIntegers.padicIntUniformEquiv (v : HeightOneSpectrum (ğ“ â„š)) :
    v.adicCompletionIntegers â„š â‰ƒáµ¤ â„¤_[primesEquiv v] :=
  let e : v.adicCompletionIntegers â„š â‰ƒáµ¤
      (Valued.v.valuationSubring : ValuationSubring (padicValuation _).Completion) :=
    (mapEquiv (valuationEquivPadicValuation v)).subtype fun _ â†¦ by
      simpa using (valuation_equiv_padicValuation v).valuedCompletion_le_one_iff
        (v.valuation_surjective â„š) (surjective_padicValuation _)
  e.trans withValIntegersUniformEquiv

theorem adicCompletionIntegers.coe_padicIntUniformEquiv_apply (v : HeightOneSpectrum (ğ“ â„š))
    (x : v.adicCompletionIntegers â„š) :
    padicIntUniformEquiv v x = adicCompletion.padicAlgEquiv v x := rfl

theorem adicCompletionIntegers.coe_padicIntUniformEquiv_symm_apply (v : HeightOneSpectrum (ğ“ â„š))
    (x : â„¤_[primesEquiv v]) :
    (adicCompletionIntegers.padicIntUniformEquiv v).symm x =
      (adicCompletion.padicUniformEquiv v).symm x := rfl

/-- `adicCompletionIntegers.padicIntUniformEquiv` as a ring isomorphism. -/
noncomputable def adicCompletionIntegers.padicIntAlgEquiv (v : HeightOneSpectrum (ğ“ â„š)) :
    v.adicCompletionIntegers â„š â‰ƒâ‚[â„¤] â„¤_[primesEquiv v] where
  __ := let e : v.adicCompletionIntegers â„š â‰ƒ+*
          (Valued.v.valuationSubring : ValuationSubring (padicValuation _).Completion) :=
        (mapRingEquiv _ (valuationEquivPadicValuation v).continuous
          (valuationEquivPadicValuation v).symm.continuous).restrict _ _ fun _ â†¦ by
          simpa using (valuation_equiv_padicValuation v).valuedCompletion_le_one_iff
            (v.valuation_surjective â„š) (surjective_padicValuation _)
      e.trans withValIntegersRingEquiv
  commutes' z := by simp

theorem adicCompletionIntegers.coe_padicIntAlgEquiv_apply (v : HeightOneSpectrum (ğ“ â„š))
    (x : v.adicCompletionIntegers â„š) :
    padicIntAlgEquiv v x = adicCompletion.padicAlgEquiv v x := rfl

theorem adicCompletionIntegers.coe_padicIntAlgEquiv_symm_apply (v : HeightOneSpectrum (ğ“ â„š))
    (x : â„¤_[primesEquiv v]) :
    (adicCompletionIntegers.padicIntAlgEquiv v).symm x =
      (adicCompletion.padicAlgEquiv v).symm x := rfl

theorem adicCompletion.padicAlgEquiv_bijOn (v : HeightOneSpectrum (ğ“ â„š)) :
    Set.BijOn (padicAlgEquiv v) (v.adicCompletionIntegers â„š) (subring (primesEquiv v)) := by
  refine âŸ¨fun x hx â†¦ ?_, (padicAlgEquiv v).injective.injOn, fun y hy â†¦ ?_âŸ©
  Â· rw [â† adicCompletionIntegers.coe_padicIntAlgEquiv_apply v âŸ¨x, hxâŸ©]
    exact norm_le_one ((adicCompletionIntegers.padicIntAlgEquiv v) âŸ¨x, hxâŸ©)
  Â· obtain âŸ¨x, hxâŸ© := (adicCompletionIntegers.padicIntAlgEquiv v).surjective âŸ¨y, hyâŸ©
    refine âŸ¨x, x.2, by rw [â† adicCompletionIntegers.coe_padicIntAlgEquiv_apply, hx]âŸ©

end Rat.HeightOneSpectrum

open Rat.HeightOneSpectrum

namespace Padic

variable (p : â„•) [Fact p.Prime]

@[simps!]
noncomputable def adicCompletionUniformEquiv :
    â„š_[p] â‰ƒáµ¤ (primesEquiv.symm âŸ¨p, Fact.outâŸ©).adicCompletion â„š :=
  (primesEquiv.apply_symm_apply âŸ¨p, _âŸ© â–¸ adicCompletion.padicUniformEquiv _).symm

noncomputable def adicCompletionAlgEquiv (p : â„•) [Fact p.Prime] :
    â„š_[p] â‰ƒâ‚[â„š] (primesEquiv.symm âŸ¨p, Fact.outâŸ©).adicCompletion â„š :=
  (primesEquiv.apply_symm_apply âŸ¨p, _âŸ© â–¸ adicCompletion.padicAlgEquiv
    (primesEquiv.symm âŸ¨p, Fact.outâŸ©)).symm

end Padic

namespace PadicInt

open Padic

variable (p : â„•) [Fact p.Prime]

@[simps!]
noncomputable def adicCompletionIntegersUniformEquiv :
    â„¤_[p] â‰ƒáµ¤ (primesEquiv.symm âŸ¨p, Fact.outâŸ©).adicCompletionIntegers â„š :=
  (primesEquiv.apply_symm_apply âŸ¨p, _âŸ© â–¸ adicCompletionIntegers.padicIntUniformEquiv _).symm

noncomputable def adicCompletionIntegersRingEqui :
    â„¤_[p] â‰ƒâ‚[â„¤] (primesEquiv.symm âŸ¨p, Fact.outâŸ©).adicCompletionIntegers â„š :=
  (primesEquiv.apply_symm_apply âŸ¨p, _âŸ© â–¸ adicCompletionIntegers.padicIntAlgEquiv _).symm

end PadicInt
