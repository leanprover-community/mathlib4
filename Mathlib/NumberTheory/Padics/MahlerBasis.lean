/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Giulio Caflisch, David Loeffler
-/
import Mathlib.Algebra.Group.ForwardDiff
import Mathlib.Analysis.Normed.Group.Ultra
import Mathlib.NumberTheory.Padics.ProperSpace
import Mathlib.RingTheory.Binomial
import Mathlib.Topology.Algebra.InfiniteSum.Nonarchimedean
import Mathlib.Topology.Algebra.Polynomial
import Mathlib.Topology.ContinuousMap.ZeroAtInfty
import Mathlib.Topology.MetricSpace.Ultra.ContinuousMaps

/-!
# The Mahler basis of continuous functions

In this file we introduce the Mahler basis function `mahler k`, for `k : ‚Ñï`, which is the unique
continuous map `‚Ñ§_[p] ‚Üí ‚Ñ§_[p]` agreeing with `n ‚Ü¶ n.choose k` for `n ‚àà ‚Ñï`.

Using this, we prove Mahler's theorem, showing that for any any continuous function `f` on `‚Ñ§_[p]`
(valued in a normed `‚Ñ§_[p]`-module `E`), the Mahler series `x ‚Ü¶ ‚àë' k, mahler k x ‚Ä¢ Œî^[n] f 0`
converges (uniformly) to `f`, and this construction defines a Banach-space isomorphism between
`C(‚Ñ§_[p], E)` and the space of sequences `‚Ñï ‚Üí E` tending to 0.

For this, we follow the argument of Bojaniƒá [bojanic74].

The formalisation of Mahler's theorem presented here is based on code written by Giulio Caflisch
for his bachelor's thesis at ETH Z√ºrich.

## References

* [R. Bojaniƒá, *A simple proof of Mahler's theorem on approximation of continuous functions of a
  p-adic variable by polynomials*][bojanic74]
* [P. Colmez, *Fonctions d'une variable p-adique*][colmez2010]

## Tags

Bojanic
-/

open Finset IsUltrametricDist NNReal Filter

open scoped fwdDiff ZeroAtInfty Topology

variable {p : ‚Ñï} [hp : Fact p.Prime]

namespace PadicInt

/-- Bound for norms of ascending Pochhammer symbols. -/
lemma norm_ascPochhammer_le (k : ‚Ñï) (x : ‚Ñ§_[p]) :
    ‚Äñ(ascPochhammer ‚Ñ§_[p] k).eval x‚Äñ ‚â§ ‚Äñ(k.factorial : ‚Ñ§_[p])‚Äñ := by
  let f := (ascPochhammer ‚Ñ§_[p] k).eval
  change ‚Äñf x‚Äñ ‚â§ ‚Äñ_‚Äñ
  have hC : (k.factorial : ‚Ñ§_[p]) ‚â† 0 := Nat.cast_ne_zero.mpr k.factorial_ne_zero
  have hf : ContinuousAt f x := Polynomial.continuousAt _
  -- find `n : ‚Ñï` such that `‚Äñf x - f n‚Äñ ‚â§ ‚Äñk!‚Äñ`
  obtain ‚ü®n, hn‚ü© : ‚àÉ n : ‚Ñï, ‚Äñf x - f n‚Äñ ‚â§ ‚Äñ(k.factorial : ‚Ñ§_[p])‚Äñ := by
    obtain ‚ü®Œ¥, hŒ¥p, hŒ¥‚ü© := Metric.continuousAt_iff.mp hf _ (norm_pos_iff.mpr hC)
    obtain ‚ü®n, hn'‚ü© := PadicInt.denseRange_natCast.exists_dist_lt x hŒ¥p
    simpa only [‚Üê dist_eq_norm_sub'] using ‚ü®n, (hŒ¥ (dist_comm x n ‚ñ∏ hn')).le‚ü©
  -- use ultrametric property to show that `‚Äñf n‚Äñ ‚â§ ‚Äñk!‚Äñ` implies `‚Äñf x‚Äñ ‚â§ ‚Äñk!‚Äñ`
  refine sub_add_cancel (f x) _ ‚ñ∏ (IsUltrametricDist.norm_add_le_max _ (f n)).trans (max_le hn ?_)
  -- finish using the fact that `n.multichoose k ‚àà ‚Ñ§`
  simp_rw [f, ‚Üê ascPochhammer_eval_cast, Polynomial.eval_eq_smeval,
    ‚Üê Ring.factorial_nsmul_multichoose_eq_ascPochhammer, smul_eq_mul, Nat.cast_mul, norm_mul]
  exact mul_le_of_le_one_right (norm_nonneg _) (norm_le_one _)

instance : IsAddTorsionFree ‚Ñ§_[p] where
  nsmul_right_injective _ := smul_right_injective ‚Ñ§_[p]

/-- The p-adic integers are a binomial ring, i.e. a ring where binomial coefficients make sense. -/
noncomputable instance instBinomialRing : BinomialRing ‚Ñ§_[p] where
  -- We define `multichoose` as a fraction in `‚Ñö_[p]` together with a proof that its norm is `‚â§ 1`.
  multichoose x k := ‚ü®(ascPochhammer ‚Ñ§_[p] k).eval x / (k.factorial : ‚Ñö_[p]), by
    rw [norm_div, div_le_one (by simpa using k.factorial_ne_zero)]
    exact x.norm_ascPochhammer_le k‚ü©
  factorial_nsmul_multichoose x k := by rw [‚Üê Subtype.coe_inj, nsmul_eq_mul, PadicInt.coe_mul,
    PadicInt.coe_natCast, mul_div_cancel‚ÇÄ _ (mod_cast k.factorial_ne_zero), Subtype.coe_inj,
    Polynomial.eval_eq_smeval, Polynomial.ascPochhammer_smeval_cast]

@[fun_prop]
lemma continuous_multichoose (k : ‚Ñï) : Continuous (fun x : ‚Ñ§_[p] ‚Ü¶ Ring.multichoose x k) := by
  simp only [Ring.multichoose, BinomialRing.multichoose, continuous_induced_rng]
  fun_prop

@[fun_prop]
lemma continuous_choose (k : ‚Ñï) : Continuous (fun x : ‚Ñ§_[p] ‚Ü¶ Ring.choose x k) := by
  simp only [Ring.choose]
  fun_prop

end PadicInt

/--
The `k`-th Mahler basis function, i.e. the unique continuous function `‚Ñ§_[p] ‚Üí ‚Ñ§_[p]`
agreeing with `n ‚Ü¶ n.choose k` for `n ‚àà ‚Ñï`. See [colmez2010], ¬ß1.2.1.
-/
noncomputable def mahler (k : ‚Ñï) : C(‚Ñ§_[p], ‚Ñ§_[p]) where
  toFun x := Ring.choose x k
  continuous_toFun := PadicInt.continuous_choose k

lemma mahler_apply (k : ‚Ñï) (x : ‚Ñ§_[p]) : mahler k x = Ring.choose x k := rfl

/-- The function `mahler k` extends `n ‚Ü¶ n.choose k` on `‚Ñï`. -/
lemma mahler_natCast_eq (k n : ‚Ñï) : mahler k (n : ‚Ñ§_[p]) = n.choose k := by
  simp only [mahler_apply, Ring.choose_natCast]

section fwdDiff

variable {M G : Type*}

/-- Bound for iterated forward differences of a continuous function from a compact space to a
nonarchimedean seminormed group. -/
lemma IsUltrametricDist.norm_fwdDiff_iter_apply_le [TopologicalSpace M] [CompactSpace M]
    [AddCommMonoid M] [SeminormedAddCommGroup G] [IsUltrametricDist G]
    (h : M) (f : C(M, G)) (m : M) (n : ‚Ñï) : ‚ÄñŒî_[h]^[n] f m‚Äñ ‚â§ ‚Äñf‚Äñ := by
  -- A proof by induction on `n` would be possible but would involve some messing around to
  -- define `Œî_[h]` as an operator on continuous maps (not just on bare functions). So instead we
  -- use the formula for `Œî_[h]^[n] f` as a sum.
  rw [fwdDiff_iter_eq_sum_shift]
  refine norm_sum_le_of_forall_le_of_nonneg (norm_nonneg f) fun i _ ‚Ü¶ ?_
  exact (norm_zsmul_le _ _).trans (f.norm_coe_le_norm _)

/-- First step in Bojaniƒá's proof of Mahler's theorem (equation (10) of [bojanic74]): rewrite
`Œî^[n + R] f 0` in a shape that makes it easy to bound `p`-adically. -/
private lemma bojanic_mahler_step1 [AddCommMonoidWithOne M] [AddCommGroup G] (f : M ‚Üí G)
    (n : ‚Ñï) {R : ‚Ñï} (hR : 1 ‚â§ R) :
    Œî_[1]^[n + R] f 0 = -‚àë j ‚àà range (R - 1), R.choose (j + 1) ‚Ä¢ Œî_[1]^[n + (j + 1)] f 0 +
      ‚àë k ‚àà range (n + 1), ((-1 : ‚Ñ§) ^ (n - k) * n.choose k) ‚Ä¢ (f (k + R) - f k) := by
  have aux : Œî_[1]^[n + R] f 0 = R.choose (R - 1 + 1) ‚Ä¢ Œî_[1]^[n + R] f 0 := by
    rw [Nat.sub_add_cancel hR, Nat.choose_self, one_smul]
  rw [neg_add_eq_sub, eq_sub_iff_add_eq, add_comm, aux, (by omega : n + R = (n + ((R - 1) + 1))),
    ‚Üê sum_range_succ, Nat.sub_add_cancel hR,
    ‚Üê sub_eq_iff_eq_add.mpr (sum_range_succ' (fun x ‚Ü¶ R.choose x ‚Ä¢ Œî_[1]^[n + x] f 0) R), add_zero,
    Nat.choose_zero_right, one_smul]
  have : ‚àë k ‚àà Finset.range (R + 1), R.choose k ‚Ä¢ Œî_[1]^[n + k] f 0 = Œî_[1]^[n] f R := by
    simpa only [‚Üê Function.iterate_add_apply, add_comm, nsmul_one, add_zero] using
      (shift_eq_sum_fwdDiff_iter 1 (Œî_[1]^[n] f) R 0).symm
  simp only [this, fwdDiff_iter_eq_sum_shift (1 : M) f n, mul_comm, nsmul_one, mul_smul, add_comm,
    add_zero, smul_sub, sum_sub_distrib]

end fwdDiff

namespace PadicInt

section norm_fwdDiff

variable {p : ‚Ñï} [hp : Fact p.Prime] {E : Type*}
  [NormedAddCommGroup E] [Module ‚Ñ§_[p] E] [IsBoundedSMul ‚Ñ§_[p] E] [IsUltrametricDist E]

/--
Second step in Bojaniƒá's proof of Mahler's theorem (equation (11) of [bojanic74]): show that values
`Œî_[1]^[n + p ^ t] f 0` for large enough `n` are bounded by the max of `(‚Äñf‚Äñ / p ^ s)` and `1 / p`
times a sup over values for smaller `n`.

We use `nnnorm`s on the RHS since `Finset.sup` requires an order with a bottom element.
-/
private lemma bojanic_mahler_step2 {f : C(‚Ñ§_[p], E)} {s t : ‚Ñï}
    (hst : ‚àÄ x y : ‚Ñ§_[p], ‚Äñx - y‚Äñ ‚â§ p ^ (-t : ‚Ñ§) ‚Üí ‚Äñf x - f y‚Äñ ‚â§ ‚Äñf‚Äñ / p ^ s) (n : ‚Ñï) :
    ‚ÄñŒî_[1]^[n + p ^ t] f 0‚Äñ ‚â§ max ‚Üë((Finset.range (p ^ t - 1)).sup
      fun j ‚Ü¶ ‚ÄñŒî_[1]^[n + (j + 1)] f 0‚Äñ‚Çä / p) (‚Äñf‚Äñ / p ^ s) := by
  -- Use previous lemma to rewrite in a convenient form.
  rw [bojanic_mahler_step1 _ _ (one_le_pow‚ÇÄ hp.out.one_le)]
  -- Now use ultrametric property and bound each term separately.
  refine (norm_add_le_max _ _).trans (max_le_max ?_ ?_)
  ¬∑ -- Bounding the sum over `range (p ^ t - 1)`: every term involves a value `Œî_[1]^[¬∑] f 0` and
    -- a binomial coefficient which is divisible by `p`
    rw [norm_neg, ‚Üê coe_nnnorm, coe_le_coe]
    refine nnnorm_sum_le_of_forall_le (fun i hi ‚Ü¶ Finset.le_sup_of_le hi ?_)
    rw [‚Üê Nat.cast_smul_eq_nsmul ‚Ñ§_[p], div_eq_inv_mul]
    refine (nnnorm_smul_le _ _).trans <| mul_le_mul_of_nonneg_right ?_ (by simp only [zero_le])
    -- remains to show norm of binomial coeff is `‚â§ p‚Åª¬π`
    rw [mem_range] at hi
    have : 0 < (p ^ t).choose (i + 1) := Nat.choose_pos (by omega)
    rw [‚Üê zpow_neg_one, ‚Üê coe_le_coe, coe_nnnorm, PadicInt.norm_eq_zpow_neg_valuation
      (mod_cast this.ne'), coe_zpow, NNReal.coe_natCast,
      zpow_le_zpow_iff_right‚ÇÄ (mod_cast hp.out.one_lt), neg_le_neg_iff,
      ‚Üê PadicInt.valuation_coe, PadicInt.coe_natCast, Padic.valuation_natCast, Nat.one_le_cast]
    exact one_le_padicValNat_of_dvd this.ne' <| hp.out.dvd_choose_pow (by omega) (by omega)
  ¬∑ -- Bounding the sum over `range (n + 1)`: every term is small by the choice of `t`
    refine norm_sum_le_of_forall_le_of_nonempty nonempty_range_add_one (fun i _ ‚Ü¶ ?_)
    calc ‚Äñ((-1 : ‚Ñ§) ^ (n - i) * n.choose i) ‚Ä¢ (f (i + ‚Üë(p ^ t)) - f i)‚Äñ
    _ ‚â§ ‚Äñ((-1 : ‚Ñ§) ^ (n - i) * n.choose i : ‚Ñ§_[p])‚Äñ * ‚Äñ(f (i + ‚Üë(p ^ t)) - f i)‚Äñ := by
      rw [‚Üê Int.cast_smul_eq_zsmul ‚Ñ§_[p]]
      exact (norm_smul_le ..).trans (by norm_cast)
    _ ‚â§ ‚Äñf (i + ‚Üë(p ^ t)) - f i‚Äñ := by
      apply mul_le_of_le_one_left (norm_nonneg _)
      simpa only [‚Üê coe_intCast] using norm_le_one _
    _ ‚â§ ‚Äñf‚Äñ / p ^ s := by
      apply hst
      rw [Nat.cast_pow, add_sub_cancel_left, norm_pow, norm_p, inv_pow, zpow_neg, zpow_natCast]

/--
Explicit bound for the decay rate of the Mahler coefficients of a continuous function on `‚Ñ§_[p]`.
This will be used to prove Mahler's theorem.
-/
lemma fwdDiff_iter_le_of_forall_le {f : C(‚Ñ§_[p], E)} {s t : ‚Ñï}
    (hst : ‚àÄ x y : ‚Ñ§_[p], ‚Äñx - y‚Äñ ‚â§ p ^ (-t : ‚Ñ§) ‚Üí ‚Äñf x - f y‚Äñ ‚â§ ‚Äñf‚Äñ / p ^ s) (n : ‚Ñï) :
    ‚ÄñŒî_[1]^[n + s * p ^ t] f 0‚Äñ ‚â§ ‚Äñf‚Äñ / p ^ s := by
  -- We show the following more general statement by induction on `k`:
  suffices ‚àÄ {k : ‚Ñï}, k ‚â§ s ‚Üí ‚ÄñŒî_[1]^[n + k * p ^ t] f 0‚Äñ ‚â§ ‚Äñf‚Äñ / p ^ k from this le_rfl
  intro k hk
  induction k generalizing n with
  | zero => -- base case just says that `‚ÄñŒî^[¬∑] (‚áëf) 0‚Äñ` is bounded by `‚Äñf‚Äñ`
    simpa only [zero_mul, pow_zero, add_zero, div_one] using norm_fwdDiff_iter_apply_le 1 f 0 n
  | succ k IH => -- induction is the "step 2" lemma above
    rw [add_mul, one_mul, ‚Üê add_assoc]
    refine (bojanic_mahler_step2 hst (n + k * p ^ t)).trans (max_le ?_ ?_)
    ¬∑ rw [‚Üê coe_nnnorm, ‚Üê NNReal.coe_natCast, ‚Üê NNReal.coe_pow, ‚Üê NNReal.coe_div, NNReal.coe_le_coe]
      refine Finset.sup_le fun j _ ‚Ü¶ ?_
      rw [pow_succ, ‚Üê div_div, div_le_div_iff_of_pos_right (mod_cast hp.out.pos), add_right_comm]
      exact_mod_cast IH (n + (j + 1)) (by omega)
    ¬∑ exact div_le_div_of_nonneg_left (norm_nonneg _)
        (mod_cast pow_pos hp.out.pos _) (mod_cast pow_le_pow_right‚ÇÄ hp.out.one_le hk)

/-- Key lemma for Mahler's theorem: for `f` a continuous function on `‚Ñ§_[p]`, the sequence
`n ‚Ü¶ Œî^[n] f 0` tends to 0. See `PadicInt.fwdDiff_iter_le_of_forall_le` for an explicit
estimate of the decay rate. -/
lemma fwdDiff_tendsto_zero (f : C(‚Ñ§_[p], E)) : Tendsto (Œî_[1]^[¬∑] f 0) atTop (ùìù 0) := by
  -- first extract an `s`
  refine NormedAddCommGroup.tendsto_nhds_zero.mpr (fun Œµ hŒµ ‚Ü¶ ?_)
  have : Tendsto (fun s ‚Ü¶ ‚Äñf‚Äñ / p ^ s) _ _ := tendsto_const_nhds.div_atTop
    (tendsto_pow_atTop_atTop_of_one_lt (mod_cast hp.out.one_lt))
  obtain ‚ü®s, hs‚ü© := (this.eventually_lt_const hŒµ).exists
  refine .mp ?_ (.of_forall fun x hx ‚Ü¶ lt_of_le_of_lt hx hs)
  -- use uniform continuity to find `t`
  obtain ‚ü®t, ht‚ü© : ‚àÉ t : ‚Ñï, ‚àÄ x y, ‚Äñx - y‚Äñ ‚â§ p ^ (-t : ‚Ñ§) ‚Üí ‚Äñf x - f y‚Äñ ‚â§ ‚Äñf‚Äñ / p ^ s := by
    rcases eq_or_ne f 0 with rfl | hf
    ¬∑ -- silly case : f = 0
      simp
    have : 0 < ‚Äñf‚Äñ / p ^ s := div_pos (norm_pos_iff.mpr hf) (mod_cast pow_pos hp.out.pos _)
    obtain ‚ü®Œ¥, hŒ¥pos, hŒ¥f‚ü© := f.uniform_continuity _ this
    obtain ‚ü®t, ht‚ü© := PadicInt.exists_pow_neg_lt p hŒ¥pos
    exact ‚ü®t, fun x y hxy ‚Ü¶  by simpa only [dist_eq_norm_sub] using (hŒ¥f (hxy.trans_lt ht)).le‚ü©
  filter_upwards [eventually_ge_atTop (s * p ^ t)] with m hm
  simpa only [Nat.sub_add_cancel hm] using fwdDiff_iter_le_of_forall_le ht (m - s * p ^ t)

end norm_fwdDiff

section mahler_coeff

variable {E : Type*} [NormedAddCommGroup E] [Module ‚Ñ§_[p] E] [IsBoundedSMul ‚Ñ§_[p] E]
  (a : E) (n : ‚Ñï) (x : ‚Ñ§_[p])

/--
A single term of a Mahler series, given by the product of the scalar-valued continuous map
`mahler n : ‚Ñ§_[p] ‚Üí ‚Ñ§_[p]` with a constant vector in some normed `‚Ñ§_[p]`-module.
-/
noncomputable def mahlerTerm : C(‚Ñ§_[p], E) := (mahler n : C(_, ‚Ñ§_[p])) ‚Ä¢ .const _ a

lemma mahlerTerm_apply : mahlerTerm a n x = mahler n x ‚Ä¢ a := by
  simp only [mahlerTerm, ContinuousMap.smul_apply', ContinuousMap.const_apply]

@[simp]
lemma norm_mahlerTerm : ‚Äñ(mahlerTerm a n : C(‚Ñ§_[p], E))‚Äñ = ‚Äña‚Äñ := by
  apply le_antisymm
  ¬∑ -- Show all values have norm ‚â§ 1
    rw [ContinuousMap.norm_le_of_nonempty]
    refine fun _ ‚Ü¶ (norm_smul_le _ _).trans <| mul_le_of_le_one_left (norm_nonneg _) (norm_le_one _)
  ¬∑ -- Show norm 1 is attained at `x = k`
    refine le_trans ?_ <| (mahlerTerm a n).norm_coe_le_norm n
    simp [mahlerTerm_apply, mahler_natCast_eq]

@[simp]
lemma mahlerTerm_one : (mahlerTerm 1 n : C(‚Ñ§_[p], ‚Ñ§_[p])) = mahler n := by
  ext; simp [mahlerTerm_apply]

/--
The uniform norm of the `k`-th Mahler basis function is 1, for every `k`.
-/
@[simp] lemma norm_mahler_eq (k : ‚Ñï) : ‚Äñ(mahler k : C(‚Ñ§_[p], ‚Ñ§_[p]))‚Äñ = 1 := by
  simp [‚Üê mahlerTerm_one]

/-- A series of the form considered in Mahler's theorem. -/
noncomputable def mahlerSeries (a : ‚Ñï ‚Üí E) : C(‚Ñ§_[p], E) := ‚àë' n, mahlerTerm (a n) n

variable [IsUltrametricDist E] [CompleteSpace E] {a : ‚Ñï ‚Üí E}

/-- A Mahler series whose coefficients tend to 0 is convergent. -/
lemma hasSum_mahlerSeries (ha : Tendsto a atTop (ùìù 0)) :
    HasSum (fun n ‚Ü¶ mahlerTerm (a n) n) (mahlerSeries a : C(‚Ñ§_[p], E)) := by
  refine (NonarchimedeanAddGroup.summable_of_tendsto_cofinite_zero ?_).hasSum
  rw [tendsto_zero_iff_norm_tendsto_zero] at ha ‚ä¢
  simpa only [norm_mahlerTerm, Nat.cofinite_eq_atTop] using ha

/-- Evaluation of a Mahler series is just the pointwise sum. -/
lemma mahlerSeries_apply (ha : Tendsto a atTop (ùìù 0)) (x : ‚Ñ§_[p]) :
    mahlerSeries a x = ‚àë' n, mahler n x ‚Ä¢ a n := by
  simp only [mahlerSeries, ‚Üê ContinuousMap.tsum_apply (hasSum_mahlerSeries ha).summable,
    mahlerTerm_apply]

/--
The value of a Mahler series at a natural number `n` is given by the finite sum of the first `m`
terms, for any `n ‚â§ m`.
-/
lemma mahlerSeries_apply_nat (ha : Tendsto a atTop (ùìù 0)) {m n : ‚Ñï} (hmn : m ‚â§ n) :
    mahlerSeries a (m : ‚Ñ§_[p]) = ‚àë i ‚àà range (n + 1), m.choose i ‚Ä¢ a i := by
  have h_van (i) : m.choose (i + (n + 1)) = 0 := Nat.choose_eq_zero_of_lt (by omega)
  have aux : Summable fun i ‚Ü¶ m.choose (i + (n + 1)) ‚Ä¢ a (i + (n + 1)) := by
    simpa only [h_van, zero_smul] using summable_zero
  simp only [mahlerSeries_apply ha, mahler_natCast_eq, Nat.cast_smul_eq_nsmul, add_zero,
    ‚Üê aux.sum_add_tsum_nat_add' (f := fun i ‚Ü¶ m.choose i ‚Ä¢ a i), h_van, zero_smul, tsum_zero]

/--
The coefficients of a Mahler series can be recovered from the sum by taking forward differences at
`0`.
-/
lemma fwdDiff_mahlerSeries (ha : Tendsto a atTop (ùìù 0)) (n) :
    Œî_[1]^[n] (mahlerSeries a) (0 : ‚Ñ§_[p]) = a n :=
  calc Œî_[1]^[n] (mahlerSeries a) 0
  -- throw away terms after the nth
  _ = Œî_[1]^[n] (fun k ‚Ü¶ ‚àë j ‚àà range (n + 1), k.choose j ‚Ä¢ (a j)) 0 := by
    simp only [fwdDiff_iter_eq_sum_shift, zero_add]
    refine Finset.sum_congr rfl fun j hj ‚Ü¶ ?_
    rw [nsmul_one, nsmul_one,
      mahlerSeries_apply_nat ha (Nat.lt_succ.mp <| Finset.mem_range.mp hj), Nat.cast_id]
  -- bring `Œî_[1]` inside sum
  _ = ‚àë j ‚àà range (n + 1), Œî_[1]^[n] (fun k ‚Ü¶ k.choose j ‚Ä¢ (a j)) 0 := by
    simp only [fwdDiff_iter_eq_sum_shift, smul_sum]
    rw [sum_comm]
  -- bring `Œî_[1]` inside scalar-mult
  _ = ‚àë j ‚àà range (n + 1), (Œî_[1]^[n] (fun k ‚Ü¶ k.choose j : ‚Ñï ‚Üí ‚Ñ§) 0) ‚Ä¢ (a j) := by
    simp only [fwdDiff_iter_eq_sum_shift, zero_add, sum_smul, smul_assoc,
      natCast_zsmul]
  -- finish using `fwdDiff_iter_choose_zero`
  _ = a n := by
    simp only [fwdDiff_iter_choose_zero, ite_smul, one_smul, zero_smul, sum_ite_eq,
      Finset.mem_range, lt_add_iff_pos_right, zero_lt_one, ‚ÜìreduceIte]

/--
**Mahler's theorem**: for any continuous function `f` from `‚Ñ§_[p]` to a `p`-adic Banach space, the
Mahler series with coefficients `n ‚Ü¶ Œî_[1]^[n] f 0` converges to the original function `f`.
-/
lemma hasSum_mahler (f : C(‚Ñ§_[p], E)) : HasSum (fun n ‚Ü¶ mahlerTerm (Œî_[1]^[n] f 0) n) f := by
  -- First show `‚àë' n, mahlerTerm f n` converges to *something*.
  have : HasSum (fun n ‚Ü¶ mahlerTerm (Œî_[1]^[n] f 0) n)
      (mahlerSeries (Œî_[1]^[¬∑] f 0) : C(‚Ñ§_[p], E)) :=
    hasSum_mahlerSeries (fwdDiff_tendsto_zero f)
  -- Now show that the sum of the Mahler terms must equal `f` on a dense set, so it is actually `f`.
  convert this using 1
  refine ContinuousMap.coe_injective (denseRange_natCast.equalizer
    (map_continuous f) (map_continuous _) (funext fun n ‚Ü¶ ?_))
  simpa [mahlerSeries_apply_nat (fwdDiff_tendsto_zero f) le_rfl]
    using shift_eq_sum_fwdDiff_iter 1 f n 0

variable (E) in
/--
The isometric equivalence from `C(‚Ñ§_[p], E)` to the space of sequences in `E` tending to `0` given
by Mahler's theorem, for `E` a nonarchimedean `‚Ñö_[p]`-Banach space.
-/
noncomputable def mahlerEquiv : C(‚Ñ§_[p], E) ‚âÉ‚Çó·µ¢[‚Ñ§_[p]] C‚ÇÄ(‚Ñï, E) where
  toFun f := ‚ü®‚ü®(Œî_[1]^[¬∑] f 0), continuous_of_discreteTopology‚ü©,
    cocompact_eq_atTop (Œ± := ‚Ñï) ‚ñ∏ fwdDiff_tendsto_zero f‚ü©
  invFun a := mahlerSeries a
  map_add' f g := by
    ext x
    simp only [ContinuousMap.coe_add, fwdDiff_iter_add, Pi.add_apply,
      ZeroAtInftyContinuousMap.coe_mk, ZeroAtInftyContinuousMap.coe_add]
  map_smul' r f := by
    ext n
    simp only [ContinuousMap.coe_smul, RingHom.id_apply, ZeroAtInftyContinuousMap.coe_mk,
      ZeroAtInftyContinuousMap.coe_smul, Pi.smul_apply, fwdDiff_iter_const_smul]
  left_inv f := (hasSum_mahler f).tsum_eq
  right_inv a := ZeroAtInftyContinuousMap.ext <|
    fwdDiff_mahlerSeries (cocompact_eq_atTop (Œ± := ‚Ñï) ‚ñ∏ zero_at_infty a)
  norm_map' f := by
    simp only [LinearEquiv.coe_mk, ‚Üê ZeroAtInftyContinuousMap.norm_toBCF_eq_norm]
    apply le_antisymm
    ¬∑ exact BoundedContinuousFunction.norm_le_of_nonempty.mpr
        (fun n ‚Ü¶ norm_fwdDiff_iter_apply_le 1 f 0 n)
    ¬∑ rw [‚Üê (hasSum_mahler f).tsum_eq]
      refine (norm_tsum_le _).trans (ciSup_le fun n ‚Ü¶ ?_)
      refine le_trans (le_of_eq ?_) (BoundedContinuousFunction.norm_coe_le_norm _ n)
      simp [(hasSum_mahler f).tsum_eq]

lemma mahlerEquiv_apply (f : C(‚Ñ§_[p], E)) : mahlerEquiv E f = fun n ‚Ü¶ Œî_[1]^[n] f 0 := rfl

lemma mahlerEquiv_symm_apply (a : C‚ÇÄ(‚Ñï, E)) : (mahlerEquiv E).symm a = (mahlerSeries (p := p) a) :=
  rfl

end mahler_coeff

end PadicInt
