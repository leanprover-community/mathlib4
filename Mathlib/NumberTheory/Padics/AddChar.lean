/-
Copyright (c) 2025 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.NumberTheory.Padics.MahlerBasis
import Mathlib.Topology.Algebra.Monoid.AddChar
import Mathlib.Analysis.SpecificLimits.Normed

/-!
# Additive characters of `â„¤_[p]`

We show that for any complete, ultrametric normed `â„¤_[p]`-algebra `R`, there is a bijection between
continuous additive characters `â„¤_[p] â†’ R` and topologically nilpotent elements of `R`, given by
sending `Îº` to the element `Îº 1 - 1`. This is used to define the Mahler transform for `p`-adic
measures.

Note that if the norm on `R` is not strictly multiplicative, then the condition that `Îº 1 - 1` be
topologically nilpotent is strictly weaker than assuming `â€–Îº 1 - 1â€– < 1`, although they are
equivalent if `NormMulClass R` holds.

##Â Main definitions and theorems:

* `addChar_of_value_at_one`: given a topologically nilpotent `r : R`, construct a continuous
  additive character of `â„¤_[p]` mapping `1` to `1 + r`.
* `continuousAddCharEquiv`: for any complete, ultrametric normed `â„¤_[p]`-algebra `R`, the map
  `addChar_of_value_at_one` defines a bijection between continuous additive characters `â„¤_[p] â†’ R`
  and topologically nilpotent elements of `R`.
* `continuousAddCharEquiv_of_norm_mul`: if the norm on `R` is strictly multiplicative (not just
  sub-multiplicative), then `addChar_of_value_at_one` is a bijection between continuous additive
  characters `â„¤_[p] â†’ R` and elements of `R` with `â€–râ€– < 1`.

##Â TODO:

* Show that the above equivalences are homeomorphisms, for appropriate choices of the topology.
-/

open scoped fwdDiff
open Filter Topology

variable {p : â„•} [Fact p.Prime]

variable {R : Type*} [NormedRing R] [Algebra â„¤_[p] R] [IsBoundedSMul â„¤_[p] R]
  [IsUltrametricDist R]

lemma AddChar.tendsto_eval_one_sub_pow {Îº : AddChar â„¤_[p] R} (hÎº : Continuous Îº) :
    Tendsto (fun n â†¦ (Îº 1 - 1) ^ n) atTop (ð“ 0) := by
  refine (PadicInt.fwdDiff_tendsto_zero âŸ¨Îº, hÎºâŸ©).congr fun n â†¦ ?_
  simpa only [AddChar.map_zero_eq_one, mul_one] using fwdDiff_addChar_eq Îº 0 1 n

namespace PadicInt
variable [CompleteSpace R]

/-- The unique continuous additive character of `â„¤_[p]` mapping `1` to `1 + r`. -/
noncomputable def addChar_of_value_at_one (r : R) (hr : Tendsto (r ^ Â·) atTop (ð“ 0)) :
    AddChar â„¤_[p] R where
  toFun := mahlerSeries (r ^ Â·)
  map_zero_eq_one' := by
    rw [â† Nat.cast_zero, mahlerSeries_apply_nat hr le_rfl, zero_add, Finset.sum_range_one,
      Nat.choose_self, pow_zero, one_smul]
  map_add_eq_mul' a b := by
    let F : C(â„¤_[p], R) := mahlerSeries (r ^ Â·)
    show F (a + b) = F a * F b
    -- It is fiddly to show directly that `F (a + b) = F a * F b` for general `a, b`,
    -- so we prove it for `a, b âˆˆ â„•` directly, and then deduce it for all `a, b` by continuity.
    have hF (n : â„•) : F n = (r + 1) ^ n := by
      rw [mahlerSeries_apply_nat hr le_rfl, (Commute.one_right _).add_pow]
      refine Finset.sum_congr rfl fun i hi â†¦ ?_
      rw [one_pow, mul_one, nsmul_eq_mul, Nat.cast_comm]
    refine congr_fun ((denseRange_natCast.prodMap denseRange_natCast).equalizer
      ((map_continuous F).comp continuous_add)
      (continuous_mul.comp (map_continuous <| F.prodMap F)) (funext fun âŸ¨m, nâŸ© â†¦ ?_)) (a, b)
    simp [â† Nat.cast_add, hF, ContinuousMap.prodMap_apply, pow_add]

@[fun_prop]
lemma continuous_addChar_of_value_at_one {r : R} (hr : Tendsto (r ^ Â·) atTop (ð“ 0)) :
    Continuous (addChar_of_value_at_one r hr : â„¤_[p] â†’ R) :=
  map_continuous (mahlerSeries (r ^ Â·))

lemma coe_addChar_of_value_at_one {r : R} (hr : Tendsto (r ^ Â·) atTop (ð“ 0)) :
    (addChar_of_value_at_one r hr : â„¤_[p] â†’ R) = mahlerSeries (r ^ Â·) :=
  rfl

@[simp]
lemma addChar_of_value_at_one_def {r : R} (hr : Tendsto (r ^ Â·) atTop (ð“ 0)) :
    addChar_of_value_at_one r hr (1 : â„¤_[p]) = 1 + r := by
  show mahlerSeries (r ^ Â·) â†‘(1 : â„•) = _
  rw [mahlerSeries_apply_nat hr le_rfl, Finset.sum_range_succ, Finset.sum_range_one,
    Nat.choose_zero_right, Nat.choose_self, one_smul, one_smul, pow_zero, pow_one]

lemma eq_addChar_of_value_at_one {r : R} (hr : Tendsto (r ^ Â·) atTop (ð“ 0))
    {Îº : AddChar â„¤_[p] R} (hÎº : Continuous Îº) (hÎº' : Îº 1 = 1 + r) :
    Îº = addChar_of_value_at_one r hr :=
  denseRange_natCast.addChar_eq_of_eval_one_eq hÎº (by fun_prop) (by simp [hÎº'])

variable (p R) in
/-- Equivalence between continuous additive characters `â„¤_[p] â†’ R`, and `r âˆˆ R` with `r ^ n â†’ 0`. -/
noncomputable def continuousAddCharEquiv :
    {Îº : AddChar â„¤_[p] R // Continuous Îº} â‰ƒ {r : R // Tendsto (r ^ Â·) atTop (ð“ 0)} where
  toFun := fun âŸ¨Îº, hÎºâŸ© â†¦ âŸ¨Îº 1 - 1, Îº.tendsto_eval_one_sub_pow hÎºâŸ©
  invFun := fun âŸ¨r, hrâŸ© â†¦ âŸ¨_, continuous_addChar_of_value_at_one hrâŸ©
  left_inv := fun âŸ¨Îº, hÎºâŸ© â†¦ by simpa using (eq_addChar_of_value_at_one _ hÎº (by abel)).symm
  right_inv := fun âŸ¨r, hrâŸ© â†¦ by simp [addChar_of_value_at_one_def hr]

@[simp] lemma continuousAddCharEquiv_apply {Îº : AddChar â„¤_[p] R} (hÎº : Continuous Îº) :
    continuousAddCharEquiv p R âŸ¨Îº, hÎºâŸ© = Îº 1 - 1 :=
  rfl

@[simp] lemma continuousAddCharEquiv_symm_apply {r : R} (hr : Tendsto (r ^ Â·) atTop (ð“ 0)) :
    (continuousAddCharEquiv p R).symm âŸ¨r, hrâŸ© =
    (addChar_of_value_at_one r hr : AddChar â„¤_[p] R) :=
  rfl

section NormMulClass
variable [NormMulClass R]

variable (p R) in
/-- Equivalence between continuous additive characters `â„¤_[p] â†’ R`, and `r âˆˆ R` with `â€–râ€– < 1`,
for rings with strictly multiplicative norm. -/
noncomputable def continuousAddCharEquiv_of_norm_mul :
    {Îº : AddChar â„¤_[p] R // Continuous Îº} â‰ƒ {r : R // â€–râ€– < 1} :=
  (continuousAddCharEquiv p R).trans <|
    .subtypeEquivProp (by simp only [tendsto_pow_atTop_nhds_zero_iff_norm_lt_one])

@[simp] lemma continuousAddCharEquiv_of_norm_mul_apply {Îº : AddChar â„¤_[p] R} (hÎº : Continuous Îº) :
    continuousAddCharEquiv_of_norm_mul p R âŸ¨Îº, hÎºâŸ© = Îº 1 - 1 :=
  rfl

@[simp] lemma continuousAddCharEquiv_of_norm_mul_symm_apply {r : R} (hr : â€–râ€– < 1) :
    (continuousAddCharEquiv_of_norm_mul p R).symm âŸ¨r, hrâŸ© = (addChar_of_value_at_one r
    (tendsto_pow_atTop_nhds_zero_iff_norm_lt_one.mpr hr) : AddChar â„¤_[p] R) :=
  rfl

end NormMulClass

end PadicInt
