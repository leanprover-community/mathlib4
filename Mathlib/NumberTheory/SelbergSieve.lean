/-
Copyright (c) 2024 Arend Mellendijk. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Arend Mellendijk
-/
import Mathlib.Analysis.Normed.Ring.Basic
import Mathlib.NumberTheory.ArithmeticFunction

/-!
# The Selberg Sieve

We set up the working assumptions of the Selberg sieve, define the notion of an upper bound sieve
and show that every upper bound sieve yields an upper bound on the size of the sifted set. We also
define the Î›Â² sieve and prove that Î›Â² sieves are upper bound sieves. We then diagonalise the main
term of the Î›Â² sieve.

We mostly follow the treatment outlined by Heath-Brown in the notes to an old graduate course. One
minor notational difference is that we write $\nu(n)$ in place of $\frac{\omega(n)}{n}$.

## Results
 * `siftedSum_le_mainSum_errSum_of_UpperBoundSieve` - Every upper bound sieve gives an upper bound
 on the size of the sifted set in terms of `mainSum` and `errSum`
 * `upperMoebius_of_lambda_sq` - Lambda squared weights produce upper bound sieves
 * `lambdaSquared_mainSum_eq_diag_quad_form` - The main sum of a Î›Â² sieve has a nice diagonalisation

## Notation
The `SelbergSieve.Notation` namespace includes common shorthand for the variables included in the
`SelbergSieve` structure.
 * `A` for `support`
 * `ğ’œ d` for `multSum d` - the combined weight of the elements of `A` that are divisible by `d`
 * `P` for `prodPrimes`
 * `a` for `weights`
 * `X` for `totalMass`
 * `Î½` for `nu`
 * `y` for `level`
 * `R d` for `rem d`
 * `g d` for `selbergTerms d`

## References

 * [Heath-Brown, *Lectures on sieves*][heathbrown2002lecturessieves]
 * [Koukoulopoulos, *The Distribution of Prime Numbers*][MR3971232]

-/

noncomputable section

open scoped BigOperators ArithmeticFunction

open Finset Real Nat

/-- We set up a sieve problem as follows. Take a finite set of natural numbers `A`, whose elements
are weighted by a sequence `a n`. Also take a finite set of primes `P`, represented by a squarefree
natural number. These are the primes that we will sift from our set `A`. Suppose we can approximate
`âˆ‘ n âˆˆ {k âˆˆ A | d âˆ£ k}, a n = Î½ d * X + R d`, where `X` is an approximation to the total size of `A`
and `Î½` is a multiplicative arithmetic function such that `0 < Î½ p < 1` for all primes `p âˆ£ P`.

Then a sieve-type theorem will give us an upper (or lower) bound on the size of the sifted sum
`âˆ‘ n âˆˆ {k âˆˆ support | k.Coprime P}, a n`, obtained by removing any elements of `A` that are a
multiple of a prime in `P`. -/
class BoundingSieve where
  /-- The set of natural numbers that is to be sifted. The fundamental lemma yields an upper bound
    on the size of this set after the multiples of small primes have been removed. -/
  support : Finset â„•
  /-- The finite set of prime numbers whose multiples are to be sifted from `support`. We work with
    their product because it lets us treat `nu` as a multiplicative arithmetic function. It also
    plays well with Moebius inversion. -/
  prodPrimes : â„•
  prodPrimes_squarefree : Squarefree prodPrimes
  /-- A sequence representing how much each element of `support` should be weighted. -/
  weights : â„• â†’ â„
  weights_nonneg : âˆ€ n : â„•, 0 â‰¤ weights n
  /-- An approximation to `âˆ‘ i in support, weights i`, i.e. the size of the unsifted set. A bad
    approximation will yield a weak statement in the final theorem. -/
  totalMass : â„
  /-- `nu d` is an approximation to the proportion of elements of `support` that are a multiple of
    `d` -/
  nu : ArithmeticFunction â„
  nu_mult : nu.IsMultiplicative
  nu_pos_of_prime : âˆ€ p : â„•, p.Prime â†’ p âˆ£ prodPrimes â†’ 0 < nu p
  nu_lt_one_of_prime : âˆ€ p : â„•, p.Prime â†’ p âˆ£ prodPrimes â†’ nu p < 1

/-- The Selberg upper bound sieve in particular introduces a parameter called the `level` which
  gives the user control over the size of the error term. -/
class SelbergSieve extends BoundingSieve where
  /-- The `level` of the sieve controls how many terms we include in the inclusion-exclusion type
    sum. A higher level will yield a tighter bound for the main term, but will also increase the
    size of the error term. -/
  level : â„
  one_le_level : 1 â‰¤ level

attribute [arith_mult] BoundingSieve.nu_mult

namespace SelbergSieve
open BoundingSieve

namespace Notation

@[inherit_doc nu]
scoped notation3 "Î½" => nu
@[inherit_doc prodPrimes]
scoped notation3 "P" => prodPrimes
@[inherit_doc weights]
scoped notation3 "a" => weights
@[inherit_doc totalMass]
scoped notation3 "X" => totalMass
@[inherit_doc support]
scoped notation3 "A" => support
@[inherit_doc level]
scoped notation3 "y" => level

theorem one_le_y [s : SelbergSieve] : 1 â‰¤ y := one_le_level

end Notation
open Notation

section Lemmas

variable [s : BoundingSieve]

/-! Lemmas about $P$. -/

theorem prodPrimes_ne_zero : P â‰  0 :=
  Squarefree.ne_zero prodPrimes_squarefree

theorem squarefree_of_dvd_prodPrimes {d : â„•} (hd : d âˆ£ P) : Squarefree d :=
  Squarefree.squarefree_of_dvd hd prodPrimes_squarefree

theorem squarefree_of_mem_divisors_prodPrimes {d : â„•} (hd : d âˆˆ divisors P) : Squarefree d := by
  simp only [Nat.mem_divisors] at hd
  exact Squarefree.squarefree_of_dvd hd.left prodPrimes_squarefree

/-! Lemmas about $\nu$. -/

theorem prod_primeFactors_nu {d : â„•} (hd : d âˆ£ P) : âˆ p âˆˆ d.primeFactors, Î½ p = Î½ d := by
  rw [â† nu_mult.map_prod_of_subset_primeFactors _ _ subset_rfl,
    Nat.prod_primeFactors_of_squarefree <| Squarefree.squarefree_of_dvd hd prodPrimes_squarefree]

theorem nu_pos_of_dvd_prodPrimes {d : â„•} (hd : d âˆ£ P) : 0 < Î½ d := by
  calc
    0 < âˆ p âˆˆ d.primeFactors, Î½ p := by
      apply prod_pos
      intro p hpd
      have hp_prime : p.Prime := prime_of_mem_primeFactors hpd
      have hp_dvd : p âˆ£ P := (dvd_of_mem_primeFactors hpd).trans hd
      exact nu_pos_of_prime p hp_prime hp_dvd
    _ = Î½ d := prod_primeFactors_nu hd

theorem nu_ne_zero {d : â„•} (hd : d âˆ£ P) : Î½ d â‰  0 := by
  apply _root_.ne_of_gt
  exact nu_pos_of_dvd_prodPrimes hd

theorem nu_lt_one_of_dvd_prodPrimes {d : â„•} (hdP : d âˆ£ P) (hd_ne_one : d â‰  1) : Î½ d < 1 := by
  have hd_sq : Squarefree d := Squarefree.squarefree_of_dvd hdP prodPrimes_squarefree
  have := hd_sq.ne_zero
  calc
    Î½ d = âˆ p âˆˆ d.primeFactors, Î½ p := (prod_primeFactors_nu hdP).symm
    _ < âˆ p âˆˆ d.primeFactors, 1 := by
      apply prod_lt_prod_of_nonempty
      Â· intro p hp
        simp only [mem_primeFactors] at hp
        apply nu_pos_of_prime p hp.1 (hp.2.1.trans hdP)
      Â· intro p hpd; rw [mem_primeFactors_of_ne_zero hd_sq.ne_zero] at hpd
        apply nu_lt_one_of_prime p hpd.left (hpd.2.trans hdP)
      Â· simp only [nonempty_primeFactors, show 1 < d by omega]
    _ = 1 := by
      simp

/-- The weight of all the elements that are a multiple of `d`. -/
@[simp]
def multSum (d : â„•) : â„ := âˆ‘ n âˆˆ A, if d âˆ£ n then a n else 0

@[inherit_doc multSum]
scoped [SelbergSieve.Notation] notation3 "ğ’œ" => multSum

/-- The remainder term in the approximation A_d = Î½ (d) X + R_d. This is the degree to which `nu`
  fails to approximate the proportion of the weight that is a multiple of `d`. -/
@[simp]
def rem (d : â„•) : â„ := ğ’œ d - Î½ d * X

@[inherit_doc rem]
scoped [SelbergSieve.Notation] notation3 "R" => rem

/-- The weight of all the elements that are not a multiple of any of our finite set of primes. -/
def siftedSum : â„ := âˆ‘ d âˆˆ A, if Coprime P d then a d else 0

/-- `X * mainSum Î¼âº` is the main term in the upper bound on `sifted_sum`. -/
def mainSum (muPlus : â„• â†’ â„) : â„ := âˆ‘ d âˆˆ divisors P, muPlus d * Î½ d

/-- `errSum Î¼âº` is the error term in the upper bound on `sifted_sum`. -/
def errSum (muPlus : â„• â†’ â„) : â„ := âˆ‘ d âˆˆ divisors P, |muPlus d| * |R d|

theorem multSum_eq_main_err (d : â„•) : multSum d = Î½ d * X + R d := by
  dsimp [rem]
  ring

theorem siftedsum_eq_sum_support_mul_ite :
    siftedSum = âˆ‘ d âˆˆ support, a d * if Nat.gcd P d = 1 then 1 else 0 := by
  dsimp only [siftedSum]
  simp_rw [mul_ite, mul_one, mul_zero]

omit s in
/-- A sequence of coefficients $\mu^{+}$ is upper Moebius if $\mu * \zeta â‰¤ \mu^{+} * \zeta$. These
  coefficients then yield an upper bound on the sifted sum. -/
def IsUpperMoebius (muPlus : â„• â†’ â„) : Prop :=
  âˆ€ n : â„•, (if n=1 then 1 else 0) â‰¤ âˆ‘ d âˆˆ n.divisors, muPlus d

theorem siftedSum_le_sum_of_upperMoebius (muPlus : â„• â†’ â„) (h : IsUpperMoebius muPlus) :
    siftedSum â‰¤ âˆ‘ d âˆˆ divisors P, muPlus d * multSum d := by
  have hÎ¼ : âˆ€ n, (if n = 1 then 1 else 0) â‰¤ âˆ‘ d âˆˆ n.divisors, muPlus d := h
  calc siftedSum â‰¤
    âˆ‘ n âˆˆ support, a n * âˆ‘ d âˆˆ (Nat.gcd P n).divisors, muPlus d := ?caseA
    _ = âˆ‘ n âˆˆ support, âˆ‘ d âˆˆ divisors P, if d âˆ£ n then a n * muPlus d else 0 := ?caseB
    _ = âˆ‘ d âˆˆ divisors P, muPlus d * multSum d := ?caseC
  case caseA =>
    rw [siftedsum_eq_sum_support_mul_ite]
    apply Finset.sum_le_sum; intro n _
    exact mul_le_mul_of_nonneg_left (hÎ¼ (Nat.gcd P n)) (weights_nonneg n)
  case caseB =>
    simp_rw [mul_sum, â† sum_filter]
    congr with n
    congr
    Â· rw [â† divisors_filter_dvd_of_dvd prodPrimes_ne_zero (Nat.gcd_dvd_left _ _)]
      ext x; simp +contextual [dvd_gcd_iff]
  case caseC =>
    rw [sum_comm]
    simp_rw [multSum, â† sum_filter, mul_sum, mul_comm]

theorem siftedSum_le_mainSum_errSum_of_upperMoebius (muPlus : â„• â†’ â„) (h : IsUpperMoebius muPlus) :
    siftedSum â‰¤ X * mainSum muPlus + errSum muPlus := by
  calc siftedSum â‰¤ âˆ‘ d âˆˆ divisors P, muPlus d * multSum d := siftedSum_le_sum_of_upperMoebius _ h
   _ â‰¤ X * âˆ‘ d âˆˆ divisors P, muPlus d * Î½ d + âˆ‘ d âˆˆ divisors P, muPlus d * R d := ?caseA
   _ â‰¤ _ := ?caseB
  case caseA =>
    apply le_of_eq
    rw [mul_sum, â†sum_add_distrib]
    congr with d
    dsimp only [rem]; ring
  case caseB =>
    apply _root_.add_le_add (le_rfl)
    apply sum_le_sum; intro d _
    rw [â†abs_mul]
    exact le_abs_self (muPlus d * R d)


end Lemmas

section LambdaSquared
/-- We consider a special class of upper bound sieves called the Î›Â² sieve. This class is
  parameterised by a sequence of real numbers. We will later choose a set of weights that minimises
  the main term, under a constraint that lets us control the error term. -/
def lambdaSquared (weights : â„• â†’ â„) : â„• â†’ â„ := fun d =>
  âˆ‘ d1 âˆˆ d.divisors, âˆ‘ d2 âˆˆ d.divisors, if d = Nat.lcm d1 d2 then weights d1 * weights d2 else 0

private theorem lambdaSquared_eq_zero_of_not_le_height_aux {w : â„• â†’ â„} {height : â„}
    (hw : âˆ€ (d : â„•), Â¬d ^ 2 â‰¤ height â†’ w d = 0) {d : â„•} (hd : Â¬â†‘d â‰¤ height) (d1 : â„•) (d2 : â„•)
    (h : d = Nat.lcm d1 d2) (hle : d1 â‰¤ d2) :
    w d1 * w d2 = 0 := by
  rw [hw d2]
  Â· ring
  by_contra hyp; apply hd
  apply le_trans _ hyp
  norm_cast
  calc _ â‰¤ d1.lcm d2 := by rw [h]
      _ â‰¤ d1 * d2 := Nat.lcm_le_mul _ _
      _ â‰¤ _       := ?_
  Â· rw [sq]; gcongr

theorem lambdaSquared_eq_zero_of_not_le_height (w : â„• â†’ â„) (height : â„)
    (hw : âˆ€ d : â„•, Â¬d ^ 2 â‰¤ height â†’ w d = 0) (d : â„•) (hd : Â¬d â‰¤ height) :
    lambdaSquared w d = 0 := by
  dsimp only [lambdaSquared]
  by_cases hheight : 0 â‰¤ height
  swap
  Â· push_neg at hd hheight
    have : âˆ€ d' : â„•, w d' = 0 := by
      intro d'; apply hw
      have : (0:â„) â‰¤ (d') ^ 2 := by norm_num
      linarith
    apply sum_eq_zero; intro d1 _
    apply sum_eq_zero; intro d2 _
    rw [this d1, this d2]
    simp only [ite_self, eq_self_iff_true, MulZeroClass.mul_zero]
  apply sum_eq_zero; intro d1 _; apply sum_eq_zero; intro d2 _
  split_ifs with h
  swap
  Â· rfl
  rcases Nat.le_or_le d1 d2 with hle | hle
  Â· apply lambdaSquared_eq_zero_of_not_le_height_aux hw hd d1 d2 h hle
  Â· rw [mul_comm]
    apply lambdaSquared_eq_zero_of_not_le_height_aux hw hd d2 d1 (Nat.lcm_comm d1 d2 â–¸ h) hle

private theorem conv_lambda_sq_larger_sum (f : â„• â†’ â„• â†’ â„• â†’ â„) (n : â„•) :
    (âˆ‘ d âˆˆ n.divisors,
        âˆ‘ d1 âˆˆ d.divisors,
          âˆ‘ d2 âˆˆ d.divisors, if d = Nat.lcm d1 d2 then f d1 d2 d else 0) =
      âˆ‘ d âˆˆ n.divisors,
        âˆ‘ d1 âˆˆ n.divisors,
          âˆ‘ d2 âˆˆ n.divisors, if d = Nat.lcm d1 d2 then f d1 d2 d else 0 := by
  apply sum_congr rfl; intro d hd
  rw [mem_divisors] at hd
  simp_rw [â†Nat.divisors_filter_dvd_of_dvd hd.2 hd.1, sum_filter, ite_sum_zero, â† ite_and]
  congr with d1
  congr with d2
  congr
  simp +contextual [â† and_assoc, eq_iff_iff, and_iff_right_iff_imp,
    Nat.dvd_lcm_left, Nat.dvd_lcm_right]

theorem upperMoebius_lambdaSquared (weights : â„• â†’ â„) (hw : weights 1 = 1) :
    IsUpperMoebius <| lambdaSquared weights := by
  dsimp [IsUpperMoebius, lambdaSquared]
  intro n
  have h_sq :
    (âˆ‘ d âˆˆ n.divisors, âˆ‘ d1 âˆˆ d.divisors, âˆ‘ d2 âˆˆ d.divisors,
      if d = Nat.lcm d1 d2 then weights d1 * weights d2 else 0) =
      (âˆ‘ d âˆˆ n.divisors, weights d) ^ 2 := by
    rw [sq, mul_sum, conv_lambda_sq_larger_sum _ n, sum_comm]
    apply sum_congr rfl; intro d1 hd1
    rw [sum_mul, sum_comm]
    apply sum_congr rfl; intro d2 hd2
    rw [sum_ite_eq_of_mem']
    ring
    rw [mem_divisors, Nat.lcm_dvd_iff]
    exact âŸ¨âŸ¨dvd_of_mem_divisors hd1, dvd_of_mem_divisors hd2âŸ©, (mem_divisors.mp hd1).2âŸ©
  rw [h_sq]
  split_ifs with hn
  Â· rw [hn]; simp [hw]
  Â· apply sq_nonneg

end LambdaSquared

section SelbergTerms

variable [s : BoundingSieve]

/-- These are the terms that appear in the sum `S` in the main term of the fundamental theorem.

$S = âˆ‘_{l|P, lâ‰¤\sqrt{y}} g(l)$ -/
def selbergTerms : ArithmeticFunction â„ :=
  nu.pmul (.prodPrimeFactors fun p =>  1 / (1 - Î½ p))

@[inherit_doc selbergTerms]
scoped [SelbergSieve.Notation] notation3 "g" => selbergTerms

theorem selbergTerms_apply (d : â„•) :
    g d = Î½ d * âˆ p âˆˆ d.primeFactors, 1 / (1 - Î½ p) := by
  unfold selbergTerms
  by_cases h : d=0
  Â· rw [h]; simp
  rw [ArithmeticFunction.pmul_apply, ArithmeticFunction.prodPrimeFactors_apply h]

/-! Now follow some important identities involving `g` -/

theorem selbergTerms_pos (l : â„•) (hl : l âˆ£ P) : 0 < g l := by
  rw [selbergTerms_apply]
  apply mul_pos <| nu_pos_of_dvd_prodPrimes hl
  apply prod_pos
  intro p hp
  rw [one_div_pos]
  have hp_prime : p.Prime := prime_of_mem_primeFactors hp
  have hp_dvd : p âˆ£ P := (Nat.dvd_of_mem_primeFactors hp).trans hl
  linarith only [nu_lt_one_of_prime p hp_prime hp_dvd]

theorem selbergTerms_isMultiplicative : ArithmeticFunction.IsMultiplicative g := by
  unfold selbergTerms
  arith_mult

theorem one_div_selbergTerms_eq_conv_moebius_nu (l : â„•) (hl : Squarefree l)
    (hnu_nonzero : Î½ l â‰  0) :
    1 / g l = âˆ‘ âŸ¨d, eâŸ© âˆˆ l.divisorsAntidiagonal, (Î¼ d) * (Î½ e)â»Â¹ :=
  by
  simp only [selbergTerms_apply, one_div, mul_inv, inv_div, inv_inv, Finset.prod_congr,
    Finset.prod_inv_distrib, (nu_mult).prodPrimeFactors_one_sub_of_squarefree _ hl, mul_sum]
  apply symm
  rw [â† Nat.sum_divisorsAntidiagonal fun i _ : â„• => (Î½ l)â»Â¹ * (â†‘(Î¼ i) * Î½ i)]
  apply sum_congr rfl; intro âŸ¨d, eâŸ© hd
  simp only [mem_divisorsAntidiagonal, ne_eq] at hd
  obtain âŸ¨rfl, _âŸ© := hd
  have : Î½ e â‰  0 := by
    revert hnu_nonzero; contrapose!
    exact nu_mult.eq_zero_of_squarefree_of_dvd_eq_zero hl (Nat.dvd_mul_left e d)
  simp only [squarefree_mul_iff] at hl âŠ¢
  field_simp
  rw [nu_mult.map_mul_of_coprime hl.1, mul_comm (Î½ d)]
  ring

theorem nu_eq_conv_one_div_selbergTerms (d : â„•) (hdP : d âˆ£ P) :
    (Î½ d)â»Â¹ = âˆ‘ l âˆˆ divisors P, if l âˆ£ d then 1 / g l else 0 := by
  apply symm
  rw [â†sum_filter, Nat.divisors_filter_dvd_of_dvd prodPrimes_ne_zero hdP]
  have hd_pos : 0 < d := Nat.pos_of_ne_zero <| ne_zero_of_dvd_ne_zero prodPrimes_ne_zero hdP
  revert hdP; revert d
  apply (ArithmeticFunction.sum_eq_iff_sum_mul_moebius_eq_on _ (fun _ _ => Nat.dvd_trans)).mpr
  intro l _ hlP
  apply symm
  exact one_div_selbergTerms_eq_conv_moebius_nu l
    (Squarefree.squarefree_of_dvd hlP prodPrimes_squarefree)
    (ne_of_gt <| nu_pos_of_dvd_prodPrimes hlP)

theorem conv_selbergTerms_eq_selbergTerms_mul_nu_inv {d : â„•} (hd : d âˆ£ P) :
    (âˆ‘ l âˆˆ divisors P, if l âˆ£ d then g l else 0) = g d * (Î½ d)â»Â¹ := by
  calc
    (âˆ‘ l âˆˆ divisors P, if l âˆ£ d then g l else 0) =
        âˆ‘ l âˆˆ divisors P, if l âˆ£ d then g (d / l) else 0 := by
      simp_rw [â† sum_filter, Nat.divisors_filter_dvd_of_dvd prodPrimes_ne_zero hd,
        sum_div_divisors d g]
    _ = g d * âˆ‘ l âˆˆ divisors P, if l âˆ£ d then 1 / g l else 0 := by
      simp_rw [â† sum_filter, mul_sum]; apply sum_congr rfl; intro l hl
      simp only [mem_filter, mem_divisors, ne_eq] at hl
      rw [selbergTerms_isMultiplicative.map_div_of_coprime hl.2]
      Â· ring
      Â· apply coprime_of_squarefree_mul <|
          (Nat.div_mul_cancel hl.2).symm â–¸ (squarefree_of_dvd_prodPrimes hd)
      Â· exact (selbergTerms_pos _ hl.1.1).ne.symm
    _ = g d * (Î½ d)â»Â¹ := by rw [â† nu_eq_conv_one_div_selbergTerms d hd]

end SelbergTerms

section QuadForm

variable [s : BoundingSieve]

/-! The main sum we get from Î›Â² coefficients is a quadratic form. We will later choose weights that
  minimise this form. -/
theorem lambdaSquared_mainSum_eq_quad_form (w : â„• â†’ â„) :
    mainSum (lambdaSquared w) =
      âˆ‘ d1 âˆˆ divisors P, âˆ‘ d2 âˆˆ divisors P,
        Î½ d1 * w d1 * Î½ d2 * w d2 * (Î½ (d1.gcd d2))â»Â¹ := by
  calc mainSum (lambdaSquared w)
      = âˆ‘ d âˆˆ divisors P, âˆ‘ d1 âˆˆ divisors d, âˆ‘ d2 âˆˆ divisors d,
          if d = d1.lcm d2 then w d1 * w d2 * Î½ d else 0 := ?caseA
    _ = âˆ‘ d âˆˆ divisors P, âˆ‘ d1 âˆˆ divisors P, âˆ‘ d2 âˆˆ divisors P,
          if d = d1.lcm d2 then w d1 * w d2 * Î½ d else 0 := by apply conv_lambda_sq_larger_sum
    _ = âˆ‘ d1 âˆˆ divisors P, âˆ‘ d2 âˆˆ divisors P,
          Î½ d1 * w d1 * Î½ d2 * w d2 * (Î½ (d1.gcd d2))â»Â¹ := ?caseB
  case caseA =>
    dsimp only [mainSum, lambdaSquared]
    rw [sum_congr rfl]; intro d _
    rw [sum_mul, sum_congr rfl]; intro d1 _
    rw [sum_mul, sum_congr rfl]; intro d2 _
    rw [ite_zero_mul]
  case caseB =>
    rw [sum_comm, sum_congr rfl]; intro d1 hd1
    rw [sum_comm, sum_congr rfl]; intro d2 hd2
    have h : d1.lcm d2 âˆ£ P := Nat.lcm_dvd_iff.mpr âŸ¨dvd_of_mem_divisors hd1, dvd_of_mem_divisors hd2âŸ©
    rw [sum_ite_eq_of_mem' (divisors P) (d1.lcm d2) _ (mem_divisors.mpr âŸ¨h, prodPrimes_ne_zeroâŸ© ),
      nu_mult.map_lcm]
    Â· ring
    refine _root_.ne_of_gt (nu_pos_of_dvd_prodPrimes ?_)
    trans d1
    Â· exact Nat.gcd_dvd_left d1 d2
    Â· exact dvd_of_mem_divisors hd1

/-! The previous quadratic form can be diagonalised with eigenvalues given by `1/g` -/
theorem lambdaSquared_mainSum_eq_diag_quad_form  (w : â„• â†’ â„) :
    mainSum (lambdaSquared w) =
      âˆ‘ l âˆˆ divisors P,
        1 / g l * (âˆ‘ d âˆˆ divisors P, if l âˆ£ d then Î½ d * w d else 0) ^ 2 := by
  calc mainSum (lambdaSquared w) =
    âˆ‘ d1 âˆˆ divisors P, âˆ‘ d2 âˆˆ divisors P, (âˆ‘ l âˆˆ divisors P,
          if l âˆ£ d1.gcd d2 then 1 / g l * (Î½ d1 * w d1) * (Î½ d2 * w d2) else 0) := ?caseA
    _ = âˆ‘ l âˆˆ divisors P, âˆ‘ d1 âˆˆ divisors P, âˆ‘ d2 âˆˆ divisors P,
        if l âˆ£ Nat.gcd d1 d2 then 1 / g l * (Î½ d1 * w d1) * (Î½ d2 * w d2) else 0 := ?caseB
    _ = âˆ‘ l âˆˆ divisors P,
        1 / g l * (âˆ‘ d âˆˆ divisors P, if l âˆ£ d then Î½ d * w d else 0) ^ 2 := ?caseC
  case caseA =>
    rw [lambdaSquared_mainSum_eq_quad_form w]
    apply sum_congr rfl; intro d1 hd1; apply sum_congr rfl; intro d2 _
    have hgcd_dvd: d1.gcd d2 âˆ£ P := (Nat.gcd_dvd_left d1 d2).trans (dvd_of_mem_divisors hd1)
    simp_rw [nu_eq_conv_one_div_selbergTerms _ hgcd_dvd, â† sum_filter, mul_sum]
    congr with l
    ring
  case caseB =>
    apply symm; rw [sum_comm, sum_congr rfl]; intro d1 _; rw [sum_comm];
  case caseC =>
    congr with l
    simp_rw [â† sum_filter, sq, sum_mul, mul_sum, sum_filter, ite_sum_zero, â† ite_and, dvd_gcd_iff]
    congr with d1
    congr with d2
    congr 1
    ring

end QuadForm

end SelbergSieve
