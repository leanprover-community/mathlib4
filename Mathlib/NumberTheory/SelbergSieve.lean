/-
Copyright (c) 2024 Arend Mellendijk. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Arend Mellendijk
-/
import Mathlib.Analysis.Normed.Ring.Basic
import Mathlib.NumberTheory.ArithmeticFunction

/-!
# The Selberg Sieve

We set up the working assumptions of the Selberg sieve, define the notion of an upper bound sieve
and show that every upper bound sieve yields an upper bound on the size of the sifted set. We also
define the Œõ¬≤ sieve and prove that Œõ¬≤ sieves are upper bound sieves. We then diagonalise the main
term of the Œõ¬≤ sieve.

We mostly follow the treatment outlined by Heath-Brown in the notes to an old graduate course. One
minor notational difference is that we write $\nu(n)$ in place of $\frac{\omega(n)}{n}$.

## Results
* `siftedSum_le_mainSum_errSum_of_UpperBoundSieve` - Every upper bound sieve gives an upper bound
  on the size of the sifted set in terms of `mainSum` and `errSum`

## Notation
The `SelbergSieve.Notation` namespace includes common shorthand for the variables included in the
`SelbergSieve` structure.
* `A` for `support`
* `ùíú d` for `multSum d` - the combined weight of the elements of `A` that are divisible by `d`
* `P` for `prodPrimes`
* `a` for `weights`
* `X` for `totalMass`
* `ŒΩ` for `nu`
* `y` for `level`
* `R d` for `rem d`
* `g d` for `selbergTerms d`

## References

* [Heath-Brown, *Lectures on sieves*][heathbrown2002lecturessieves]
* [Koukoulopoulos, *The Distribution of Prime Numbers*][MR3971232]

-/

noncomputable section

open scoped BigOperators ArithmeticFunction

open Finset Real Nat

/-- We set up a sieve problem as follows. Take a finite set of natural numbers `A`, whose elements
are weighted by a sequence `a n`. Also take a finite set of primes `P`, represented by a squarefree
natural number. These are the primes that we will sift from our set `A`. Suppose we can approximate
`‚àë n ‚àà {k ‚àà A | d ‚à£ k}, a n = ŒΩ d * X + R d`, where `X` is an approximation to the total size of `A`
and `ŒΩ` is a multiplicative arithmetic function such that `0 < ŒΩ p < 1` for all primes `p ‚à£ P`.

Then a sieve-type theorem will give us an upper (or lower) bound on the size of the sifted sum
`‚àë n ‚àà {k ‚àà support | k.Coprime P}, a n`, obtained by removing any elements of `A` that are a
multiple of a prime in `P`. -/
structure BoundingSieve where
  /-- The set of natural numbers that is to be sifted. The fundamental lemma yields an upper bound
    on the size of this set after the multiples of small primes have been removed. -/
  support : Finset ‚Ñï
  /-- The finite set of prime numbers whose multiples are to be sifted from `support`. We work with
    their product because it lets us treat `nu` as a multiplicative arithmetic function. It also
    plays well with Moebius inversion. -/
  prodPrimes : ‚Ñï
  prodPrimes_squarefree : Squarefree prodPrimes
  /-- A sequence representing how much each element of `support` should be weighted. -/
  weights : ‚Ñï ‚Üí ‚Ñù
  weights_nonneg : ‚àÄ n : ‚Ñï, 0 ‚â§ weights n
  /-- An approximation to `‚àë i in support, weights i`, i.e. the size of the unsifted set. A bad
    approximation will yield a weak statement in the final theorem. -/
  totalMass : ‚Ñù
  /-- `nu d` is an approximation to the proportion of elements of `support` that are a multiple of
    `d` -/
  nu : ArithmeticFunction ‚Ñù
  nu_mult : nu.IsMultiplicative
  nu_pos_of_prime : ‚àÄ p : ‚Ñï, p.Prime ‚Üí p ‚à£ prodPrimes ‚Üí 0 < nu p
  nu_lt_one_of_prime : ‚àÄ p : ‚Ñï, p.Prime ‚Üí p ‚à£ prodPrimes ‚Üí nu p < 1

/-- The Selberg upper bound sieve in particular introduces a parameter called the `level` which
  gives the user control over the size of the error term. -/
structure SelbergSieve extends BoundingSieve where
  /-- The `level` of the sieve controls how many terms we include in the inclusion-exclusion type
    sum. A higher level will yield a tighter bound for the main term, but will also increase the
    size of the error term. -/
  level : ‚Ñù
  one_le_level : 1 ‚â§ level

attribute [arith_mult] BoundingSieve.nu_mult

namespace Mathlib.Meta.Positivity

open Lean Meta Qq

/-- Extension for the `positivity` tactic: `BoundingSieve.weights`. -/
@[positivity BoundingSieve.weights _ _]
def evalBoundingSieveWeights : PositivityExt where eval {u Œ±} _zŒ± _pŒ± e := do
  match u, Œ±, e with
  | 0, ~q(‚Ñù), ~q(@BoundingSieve.weights $s $n) =>
    assertInstancesCommute
    pure (.nonnegative q(BoundingSieve.weights_nonneg $s $n))
  | _, _, _ => throwError "not BoundingSieve.weights"

end Mathlib.Meta.Positivity

open BoundingSieve

variable {s : BoundingSieve}

-- private abbrev BoundingSieve.ŒΩ := BoundingSieve.nu

-- private abbrev BoundingSieve.P := BoundingSieve.prodPrimes

set_option quotPrecheck false
set_option hygiene false

@[inherit_doc nu]
local macro "ŒΩ" : term => `(s.nu)
@[inherit_doc prodPrimes]
local macro "P" : term => `(s.prodPrimes)
@[inherit_doc weights]
local macro "a" : term => `(s.weights)
@[inherit_doc totalMass]
local macro "X" : term => `(s.totalMass)
@[inherit_doc support]
local macro "A" : term => `(s.support)

@[inherit_doc SelbergSieve.level]
local macro "y" : term => `(s.level)

open Lean PrettyPrinter Delaborator SubExpr

@[local app_unexpander nu, inherit_doc nu]
def unexpanderNu : Unexpander
  | `($_nu $_s) => `(ŒΩ)
  | `($fn) => `($fn)

@[local app_unexpander prodPrimes, inherit_doc prodPrimes]
def unexpanderProdPrimes : Unexpander
  | `($_ $_s) => `(P)
  | `($fn) => `($fn)

@[local app_unexpander weights, inherit_doc weights]
def unexpanderWeights : Unexpander
  | `($_ $_s $n) => `(a $n)
  | `($_ $_s) => `(a)
  | `($fn) => `($fn)

@[local app_unexpander totalMass, inherit_doc totalMass]
def unexpanderTotalMass : Unexpander
  | `($_ $_s) => `(X)
  | `($fn) => `($fn)

@[local app_unexpander support, inherit_doc support]
def unexpanderSupport : Unexpander
  | `($_ $_s) => `(A)
  | `($fn) => `($fn)

@[local app_unexpander SelbergSieve.level, inherit_doc SelbergSieve.level]
def unexpanderLevel : Unexpander
  | `($_level $_s) => `(y)
  | `($level) => `($level)



theorem SelbergSieve.one_le_y {s : SelbergSieve} : 1 ‚â§ y := s.one_le_level


namespace BoundingSieve
/-! Lemmas about $P$. -/

theorem prodPrimes_ne_zero : P ‚â† 0 := by
  apply Squarefree.ne_zero s.prodPrimes_squarefree

theorem squarefree_of_dvd_prodPrimes {d : ‚Ñï} (hd : d ‚à£ P) : Squarefree d :=
  Squarefree.squarefree_of_dvd hd s.prodPrimes_squarefree

theorem squarefree_of_mem_divisors_prodPrimes {d : ‚Ñï} (hd : d ‚àà divisors P) : Squarefree d := by
  simp only [Nat.mem_divisors] at hd
  exact Squarefree.squarefree_of_dvd hd.left s.prodPrimes_squarefree

/-! Lemmas about $\nu$. -/

theorem prod_primeFactors_nu {d : ‚Ñï} (hd : d ‚à£ P) : ‚àè p ‚àà d.primeFactors, ŒΩ p = ŒΩ d := by
  rw [‚Üê s.nu_mult.map_prod_of_subset_primeFactors _ _ subset_rfl,
    Nat.prod_primeFactors_of_squarefree <| Squarefree.squarefree_of_dvd hd s.prodPrimes_squarefree]

theorem nu_pos_of_dvd_prodPrimes {d : ‚Ñï} (hd : d ‚à£ P) : 0 < ŒΩ d := by
  calc
    0 < ‚àè p ‚àà d.primeFactors, ŒΩ p := by
      apply prod_pos
      intro p hpd
      have hp_prime : p.Prime := prime_of_mem_primeFactors hpd
      have hp_dvd : p ‚à£ P := (dvd_of_mem_primeFactors hpd).trans hd
      exact s.nu_pos_of_prime p hp_prime hp_dvd
    _ = ŒΩ d := prod_primeFactors_nu hd

theorem nu_ne_zero {d : ‚Ñï} (hd : d ‚à£ P) : ŒΩ d ‚â† 0 := by
  apply _root_.ne_of_gt
  exact nu_pos_of_dvd_prodPrimes hd

theorem nu_lt_one_of_dvd_prodPrimes {d : ‚Ñï} (hdP : d ‚à£ P) (hd_ne_one : d ‚â† 1) : ŒΩ d < 1 := by
  have hd_sq : Squarefree d := Squarefree.squarefree_of_dvd hdP s.prodPrimes_squarefree
  have := hd_sq.ne_zero
  calc
    ŒΩ d = ‚àè p ‚àà d.primeFactors, ŒΩ p := (prod_primeFactors_nu hdP).symm
    _ < ‚àè p ‚àà d.primeFactors, 1 := by
      apply prod_lt_prod_of_nonempty
      ¬∑ intro p hp
        simp only [mem_primeFactors] at hp
        apply s.nu_pos_of_prime p hp.1 (hp.2.1.trans hdP)
      ¬∑ intro p hpd; rw [mem_primeFactors_of_ne_zero hd_sq.ne_zero] at hpd
        apply s.nu_lt_one_of_prime p hpd.left (hpd.2.trans hdP)
      ¬∑ simp only [nonempty_primeFactors, show 1 < d by omega]
    _ = 1 := by
      simp

variable (s) in
/-- The weight of all the elements that are a multiple of `d`. -/
@[simp]
def multSum (d : ‚Ñï) : ‚Ñù := ‚àë n ‚àà A, if d ‚à£ n then a n else 0

@[inherit_doc multSum]
local macro "ùíú" : term => `(s.multSum)

@[local app_unexpander multSum, inherit_doc multSum]
def unexpanderMultSum : Unexpander
  | `($_ $_s $n) => `(ùíú $n)
  | `($_ $_s) => `(ùíú)
  | `($fn) => `($fn)

variable (s) in
/-- The remainder term in the approximation A_d = ŒΩ (d) X + R_d. This is the degree to which `nu`
  fails to approximate the proportion of the weight that is a multiple of `d`. -/
@[simp]
def rem (d : ‚Ñï) : ‚Ñù := ùíú d - ŒΩ d * X

@[inherit_doc rem]
local macro "R" : term => `(s.rem)

@[local app_unexpander rem, inherit_doc rem]
def unexpanderRem : Unexpander
  | `($_ $_s $n) => `(R $n)
  | `($_ $_s) => `(R)
  | `($fn) => `($fn)

/-- The weight of all the elements that are not a multiple of any of our finite set of primes. -/
def siftedSum : ‚Ñù := ‚àë d ‚àà A, if Coprime P d then a d else 0

/-- `X * mainSum Œº‚Å∫` is the main term in the upper bound on `sifted_sum`. -/
def mainSum (muPlus : ‚Ñï ‚Üí ‚Ñù) : ‚Ñù := ‚àë d ‚àà divisors P, muPlus d * ŒΩ d

/-- `errSum Œº‚Å∫` is the error term in the upper bound on `sifted_sum`. -/
def errSum (muPlus : ‚Ñï ‚Üí ‚Ñù) : ‚Ñù := ‚àë d ‚àà divisors P, |muPlus d| * |R d|

theorem multSum_eq_main_err (d : ‚Ñï) : ùíú d = ŒΩ d * X + R d := by
  dsimp [rem]
  ring

theorem siftedsum_eq_sum_support_mul_ite :
    s.siftedSum = ‚àë d ‚àà A, a d * if Nat.gcd P d = 1 then 1 else 0 := by
  dsimp only [siftedSum]
  simp_rw [mul_ite, mul_one, mul_zero]

omit s in
/-- A sequence of coefficients $\mu^{+}$ is upper Moebius if $\mu * \zeta ‚â§ \mu^{+} * \zeta$. These
  coefficients then yield an upper bound on the sifted sum. -/
def IsUpperMoebius (muPlus : ‚Ñï ‚Üí ‚Ñù) : Prop :=
  ‚àÄ n : ‚Ñï, (if n=1 then 1 else 0) ‚â§ ‚àë d ‚àà n.divisors, muPlus d


theorem siftedSum_le_sum_of_upperMoebius (muPlus : ‚Ñï ‚Üí ‚Ñù) (h : IsUpperMoebius muPlus) :
    s.siftedSum ‚â§ ‚àë d ‚àà divisors P, muPlus d * ùíú d := by
  have hŒº : ‚àÄ n, (if n = 1 then 1 else 0) ‚â§ ‚àë d ‚àà n.divisors, muPlus d := h
  calc siftedSum ‚â§
    ‚àë n ‚àà A, a n * ‚àë d ‚àà (Nat.gcd P n).divisors, muPlus d := ?caseA
    _ = ‚àë n ‚àà A, ‚àë d ‚àà divisors P, if d ‚à£ n then a n * muPlus d else 0 := ?caseB
    _ = ‚àë d ‚àà divisors P, muPlus d * ùíú d := ?caseC
  case caseA =>
    rw [siftedsum_eq_sum_support_mul_ite]
    gcongr with n
    exact hŒº (Nat.gcd P n)
  case caseB =>
    simp_rw [mul_sum, ‚Üê sum_filter]
    congr with n
    congr
    ¬∑ rw [‚Üê divisors_filter_dvd_of_dvd prodPrimes_ne_zero (Nat.gcd_dvd_left _ _)]
      ext x; simp +contextual [dvd_gcd_iff]
  case caseC =>
    rw [sum_comm]
    simp_rw [multSum, ‚Üê sum_filter, mul_sum, mul_comm]

theorem siftedSum_le_mainSum_errSum_of_upperMoebius (muPlus : ‚Ñï ‚Üí ‚Ñù) (h : IsUpperMoebius muPlus) :
    s.siftedSum ‚â§ X * s.mainSum muPlus + s.errSum muPlus := calc
  siftedSum ‚â§ ‚àë d ‚àà divisors P, muPlus d * ùíú d :=
    siftedSum_le_sum_of_upperMoebius _ h
  _ = X * mainSum muPlus + ‚àë d ‚àà divisors P, muPlus d * R d := by
    rw [mainSum, mul_sum, ‚Üêsum_add_distrib]
    congr with d
    dsimp only [rem]; ring
  _ ‚â§ X * mainSum muPlus + errSum muPlus := by
    rw [errSum]
    gcongr _ + ‚àë d ‚àà _, ?_ with d
    rw [‚Üêabs_mul]
    exact le_abs_self (muPlus d * R d)

end BoundingSieve
