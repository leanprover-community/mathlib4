/-
Copyright (c) 2016 Microsoft Corporation. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Leonardo de Moura
-/
import Lean.Parser.Term
import Batteries.Util.ExtendedBinder
import Mathlib.Mathport.Rename

/-!

# Sets

This file sets up the theory of sets whose elements have a given type.

## Main definitions

Given a type `X` and a predicate `p : X ‚Üí Prop`:

* `Set X` : the type of sets whose elements have type `X`
* `{a : X | p a} : Set X` : the set of all elements of `X` satisfying `p`
* `{a | p a} : Set X` : a more concise notation for `{a : X | p a}`
* `{f x y | (x : X) (y : Y)} : Set Z` : a more concise notation for `{z : Z | ‚àÉ x y, f x y = z}`
* `{a ‚àà S | p a} : Set X` : given `S : Set X`, the subset of `S` consisting of
   its elements satisfying `p`.

## Implementation issues

As in Lean 3, `Set X := X ‚Üí Prop`

I didn't call this file Data.Set.Basic because it contains core Lean 3
stuff which happens before mathlib3's data.set.basic .
This file is a port of the core Lean 3 file `lib/lean/library/init/data/set.lean`.

-/

open Batteries.ExtendedBinder

set_option autoImplicit true

/-- A set is a collection of elements of some type `Œ±`.

Although `Set` is defined as `Œ± ‚Üí Prop`, this is an implementation detail which should not be
relied on. Instead, `setOf` and membership of a set (`‚àà`) should be used to convert between sets
and predicates.
-/
def Set (Œ± : Type u) := Œ± ‚Üí Prop
#align set Set

/-- Turn a predicate `p : Œ± ‚Üí Prop` into a set, also written as `{x | p x}` -/
def setOf {Œ± : Type u} (p : Œ± ‚Üí Prop) : Set Œ± :=
  p
#align set_of setOf

namespace Set

/-- Membership in a set -/
protected def Mem (a : Œ±) (s : Set Œ±) : Prop :=
  s a

instance : Membership Œ± (Set Œ±) :=
  ‚ü®Set.Mem‚ü©

theorem ext {a b : Set Œ±} (h : ‚àÄ (x : Œ±), x ‚àà a ‚Üî x ‚àà b) : a = b :=
  funext (fun x ‚Ü¶ propext (h x))


/-- The subset relation on sets. `s ‚äÜ t` means that all elements of `s` are elements of `t`.

Note that you should **not** use this definition directly, but instead write `s ‚äÜ t`. -/
protected def Subset (s‚ÇÅ s‚ÇÇ : Set Œ±) :=
  ‚àÄ ‚¶Éa‚¶Ñ, a ‚àà s‚ÇÅ ‚Üí a ‚àà s‚ÇÇ

/-- Porting note: we introduce `‚â§` before `‚äÜ` to help the unifier when applying lattice theorems
to subset hypotheses. -/
instance : LE (Set Œ±) :=
  ‚ü®Set.Subset‚ü©

instance : HasSubset (Set Œ±) :=
  ‚ü®(¬∑ ‚â§ ¬∑)‚ü©

instance : EmptyCollection (Set Œ±) :=
  ‚ü®fun _ ‚Ü¶ False‚ü©

syntax "{" extBinder " | " term "}" : term

macro_rules
  | `({ $x:ident | $p }) => `(setOf fun $x:ident ‚Ü¶ $p)
  | `({ $x:ident : $t | $p }) => `(setOf fun $x:ident : $t ‚Ü¶ $p)
  | `({ $x:ident $b:binderPred | $p }) =>
    `(setOf fun $x:ident ‚Ü¶ satisfies_binder_pred% $x $b ‚àß $p)

@[app_unexpander setOf]
def setOf.unexpander : Lean.PrettyPrinter.Unexpander
  | `($_ fun $x:ident ‚Ü¶ $p) => `({ $x:ident | $p })
  | `($_ fun ($x:ident : $ty:term) ‚Ü¶ $p) => `({ $x:ident : $ty:term | $p })
  | _ => throw ()

open Batteries.ExtendedBinder in
/--
`{ f x y | (x : X) (y : Y) }` is notation for the set of elements `f x y` constructed from the
binders `x` and `y`, equivalent to `{z : Z | ‚àÉ x y, f x y = z}`.

If `f x y` is a single identifier, it must be parenthesized to avoid ambiguity with `{x | p x}`;
for instance, `{(x) | (x : Nat) (y : Nat) (_hxy : x = y^2)}`.
-/
macro (priority := low) "{" t:term " | " bs:extBinders "}" : term =>
  `({x | ‚àÉ·µâ $bs:extBinders, $t = x})

/--
* `{ pat : X | p }` is notation for pattern matching in set-builder notation,
  where `pat` is a pattern that is matched by all objects of type `X`
  and `p` is a proposition that can refer to variables in the pattern.
  It is the set of all objects of type `X` which, when matched with the pattern `pat`,
  make `p` come out true.
* `{ pat | p }` is the same, but in the case when the type `X` can be inferred.

For example, `{ (m, n) : ‚Ñï √ó ‚Ñï | m * n = 12 }` denotes the set of all ordered pairs of
natural numbers whose product is 12.

Note that if the type ascription is left out and `p` can be interpreted as an extended binder,
then the extended binder interpretation will be used.  For example, `{ n + 1 | n < 3 }` will
be interpreted as `{ x : Nat | ‚àÉ n < 3, n + 1 = x }` rather than using pattern matching.
-/
macro (name := macroPattSetBuilder) (priority := low-1)
  "{" pat:term " : " t:term " | " p:term "}" : term =>
  `({ x : $t | match x with | $pat => $p })

@[inherit_doc macroPattSetBuilder]
macro (priority := low-1) "{" pat:term " | " p:term "}" : term =>
  `({ x | match x with | $pat => $p })

/-- Pretty printing for set-builder notation with pattern matching. -/
@[app_unexpander setOf]
def setOfPatternMatchUnexpander : Lean.PrettyPrinter.Unexpander
  | `($_ fun $x:ident ‚Ü¶ match $y:ident with | $pat => $p) =>
      if x == y then
        `({ $pat:term | $p:term })
      else
        throw ()
  | `($_ fun ($x:ident : $ty:term) ‚Ü¶ match $y:ident with | $pat => $p) =>
      if x == y then
        `({ $pat:term : $ty:term | $p:term })
      else
        throw ()
  | _ => throw ()

/-- The universal set on a type `Œ±` is the set containing all elements of `Œ±`.

This is conceptually the "same as" `Œ±` (in set theory, it is actually the same), but type theory
makes the distinction that `Œ±` is a type while `Set.univ` is a term of type `Set Œ±`. `Set.univ` can
itself be coerced to a type `‚Ü•Set.univ` which is in bijection with (but distinct from) `Œ±`. -/
def univ : Set Œ± := {_a | True}
#align set.univ Set.univ

/-- `Set.insert a s` is the set `{a} ‚à™ s`.

Note that you should **not** use this definition directly, but instead write `insert a s` (which is
mediated by the `Insert` typeclass). -/
protected def insert (a : Œ±) (s : Set Œ±) : Set Œ± := {b | b = a ‚à® b ‚àà s}

instance : Insert Œ± (Set Œ±) := ‚ü®Set.insert‚ü©

/-- The singleton of an element `a` is the set with `a` as a single element.

Note that you should **not** use this definition directly, but instead write `{a}`. -/
protected def singleton (a : Œ±) : Set Œ± := {b | b = a}

instance instSingletonSet : Singleton Œ± (Set Œ±) := ‚ü®Set.singleton‚ü©

/-- The union of two sets `s` and `t` is the set of elements contained in either `s` or `t`.

Note that you should **not** use this definition directly, but instead write `s ‚à™ t`. -/
protected def union (s‚ÇÅ s‚ÇÇ : Set Œ±) : Set Œ± := {a | a ‚àà s‚ÇÅ ‚à® a ‚àà s‚ÇÇ}

instance : Union (Set Œ±) := ‚ü®Set.union‚ü©

/-- The intersection of two sets `s` and `t` is the set of elements contained in both `s` and `t`.

Note that you should **not** use this definition directly, but instead write `s ‚à© t`. -/
protected def inter (s‚ÇÅ s‚ÇÇ : Set Œ±) : Set Œ± := {a | a ‚àà s‚ÇÅ ‚àß a ‚àà s‚ÇÇ}

instance : Inter (Set Œ±) := ‚ü®Set.inter‚ü©

/-- The complement of a set `s` is the set of elements not contained in `s`.

Note that you should **not** use this definition directly, but instead write `s·∂ú`. -/
protected def compl (s : Set Œ±) : Set Œ± := {a | a ‚àâ s}

/-- The difference of two sets `s` and `t` is the set of elements contained in `s` but not in `t`.

Note that you should **not** use this definition directly, but instead write `s \ t`. -/
protected def diff (s t : Set Œ±) : Set Œ± := {a ‚àà s | a ‚àâ t}

instance : SDiff (Set Œ±) := ‚ü®Set.diff‚ü©

/-- `ùí´ s` is the set of all subsets of `s`. -/
def powerset (s : Set Œ±) : Set (Set Œ±) := {t | t ‚äÜ s}

@[inherit_doc] prefix:100 "ùí´" => powerset

/-- The image of `s : Set Œ±` by `f : Œ± ‚Üí Œ≤`, written `f '' s`, is the set of `b : Œ≤` such that
`f a = b` for some `a ‚àà s`. -/
def image (f : Œ± ‚Üí Œ≤) (s : Set Œ±) : Set Œ≤ := {f a | a ‚àà s}

instance : Functor Set where map := @Set.image

instance : LawfulFunctor Set where
  id_map _ := funext fun _ ‚Ü¶ propext ‚ü®fun ‚ü®_, sb, rfl‚ü© ‚Ü¶ sb, fun sb ‚Ü¶ ‚ü®_, sb, rfl‚ü©‚ü©
  comp_map g h _ := funext <| fun c ‚Ü¶ propext
    ‚ü®fun ‚ü®a, ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©‚ü© ‚Ü¶ ‚ü®g a, ‚ü®‚ü®a, ‚ü®h‚ÇÅ, rfl‚ü©‚ü©, h‚ÇÇ‚ü©‚ü©,
     fun ‚ü®_, ‚ü®‚ü®a, ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©‚ü©, h‚ÇÉ‚ü©‚ü© ‚Ü¶ ‚ü®a, ‚ü®h‚ÇÅ, show h (g a) = c from h‚ÇÇ ‚ñ∏ h‚ÇÉ‚ü©‚ü©‚ü©
  map_const := rfl

/-- The property `s.Nonempty` expresses the fact that the set `s` is not empty. It should be used
in theorem assumptions instead of `‚àÉ x, x ‚àà s` or `s ‚â† ‚àÖ` as it gives access to a nice API thanks
to the dot notation. -/
protected def Nonempty (s : Set Œ±) : Prop :=
  ‚àÉ x, x ‚àà s
#align set.nonempty Set.Nonempty

end Set
