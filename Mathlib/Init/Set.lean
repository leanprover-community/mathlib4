/-
Copyright (c) 2016 Microsoft Corporation. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Leonardo de Moura
-/
import Lean.Parser.Term
import Batteries.Util.ExtendedBinder
import Mathlib.Mathport.Rename

/-!

# Sets

This file sets up the theory of sets whose elements have a given type.

## Main definitions

Given a type `X` and a predicate `p : X â†’ Prop`:

* `Set X` : the type of sets whose elements have type `X`
* `{a : X | p a} : Set X` : the set of all elements of `X` satisfying `p`
* `{a | p a} : Set X` : a more concise notation for `{a : X | p a}`
* `{f x y | (x : X) (y : Y)} : Set Z` : a more concise notation for `{z : Z | âˆƒ x y, f x y = z}`
* `{a âˆˆ S | p a} : Set X` : given `S : Set X`, the subset of `S` consisting of
   its elements satisfying `p`.

## Implementation issues

As in Lean 3, `Set X := X â†’ Prop`

I didn't call this file Data.Set.Basic because it contains core Lean 3
stuff which happens before mathlib3's data.set.basic .
This file is a port of the core Lean 3 file `lib/lean/library/init/data/set.lean`.

-/

open Batteries.ExtendedBinder

set_option autoImplicit true

/-- A set is a collection of elements of some type `Î±`.

Although `Set` is defined as `Î± â†’ Prop`, this is an implementation detail which should not be
relied on. Instead, `setOf` and membership of a set (`âˆˆ`) should be used to convert between sets
and predicates.
-/
structure Set (Î± : Type u) where
  ofPred ::
  /-- The predicate defining the set -/
  toPred : Î± â†’ Prop
#align set Set

/-- Turn a predicate `p : Î± â†’ Prop` into a set, also written as `{x | p x}` -/
def setOf {Î± : Type u} (p : Î± â†’ Prop) : Set Î± :=
  âŸ¨pâŸ©
#align set_of setOf

namespace Set

/-- Membership in a set -/
protected def Mem (a : Î±) (s : Set Î±) : Prop :=
  s.toPred a

instance : Membership Î± (Set Î±) :=
  âŸ¨Set.MemâŸ©

theorem ext {a b : Set Î±} (h : âˆ€ (x : Î±), x âˆˆ a â†” x âˆˆ b) : a = b :=
  congrArg ofPred <| funext (fun x â†¦ propext (h x))


/-- The subset relation on sets. `s âŠ† t` means that all elements of `s` are elements of `t`.

Note that you should **not** use this definition directly, but instead write `s âŠ† t`. -/
protected def Subset (sâ‚ sâ‚‚ : Set Î±) :=
  âˆ€ â¦ƒaâ¦„, a âˆˆ sâ‚ â†’ a âˆˆ sâ‚‚

/-- Porting note: we introduce `â‰¤` before `âŠ†` to help the unifier when applying lattice theorems
to subset hypotheses. -/
instance : LE (Set Î±) :=
  âŸ¨Set.SubsetâŸ©

instance : HasSubset (Set Î±) :=
  âŸ¨(Â· â‰¤ Â·)âŸ©

instance : EmptyCollection (Set Î±) where
  emptyCollection := âŸ¨fun _ â†¦ FalseâŸ©

syntax "{" extBinder " | " term "}" : term

macro_rules
  | `({ $x:ident | $p }) => `(setOf fun $x:ident â†¦ $p)
  | `({ $x:ident : $t | $p }) => `(setOf fun $x:ident : $t â†¦ $p)
  | `({ $x:ident $b:binderPred | $p }) =>
    `(setOf fun $x:ident â†¦ satisfies_binder_pred% $x $b âˆ§ $p)

@[app_unexpander setOf]
def setOf.unexpander : Lean.PrettyPrinter.Unexpander
  | `($_ fun $x:ident â†¦ $p) => `({ $x:ident | $p })
  | `($_ fun ($x:ident : $ty:term) â†¦ $p) => `({ $x:ident : $ty:term | $p })
  | _ => throw ()

open Batteries.ExtendedBinder in
/--
`{ f x y | (x : X) (y : Y) }` is notation for the set of elements `f x y` constructed from the
binders `x` and `y`, equivalent to `{z : Z | âˆƒ x y, f x y = z}`.

If `f x y` is a single identifier, it must be parenthesized to avoid ambiguity with `{x | p x}`;
for instance, `{(x) | (x : Nat) (y : Nat) (_hxy : x = y^2)}`.
-/
macro (priority := low) "{" t:term " | " bs:extBinders "}" : term =>
  `({x | âˆƒáµ‰ $bs:extBinders, $t = x})

/--
* `{ pat : X | p }` is notation for pattern matching in set-builder notation,
  where `pat` is a pattern that is matched by all objects of type `X`
  and `p` is a proposition that can refer to variables in the pattern.
  It is the set of all objects of type `X` which, when matched with the pattern `pat`,
  make `p` come out true.
* `{ pat | p }` is the same, but in the case when the type `X` can be inferred.

For example, `{ (m, n) : â„• Ã— â„• | m * n = 12 }` denotes the set of all ordered pairs of
natural numbers whose product is 12.

Note that if the type ascription is left out and `p` can be interpreted as an extended binder,
then the extended binder interpretation will be used.  For example, `{ n + 1 | n < 3 }` will
be interpreted as `{ x : Nat | âˆƒ n < 3, n + 1 = x }` rather than using pattern matching.
-/
macro (name := macroPattSetBuilder) (priority := low-1)
  "{" pat:term " : " t:term " | " p:term "}" : term =>
  `({ x : $t | match x with | $pat => $p })

@[inherit_doc macroPattSetBuilder]
macro (priority := low-1) "{" pat:term " | " p:term "}" : term =>
  `({ x | match x with | $pat => $p })

/-- Pretty printing for set-builder notation with pattern matching. -/
@[app_unexpander setOf]
def setOfPatternMatchUnexpander : Lean.PrettyPrinter.Unexpander
  | `($_ fun $x:ident â†¦ match $y:ident with | $pat => $p) =>
      if x == y then
        `({ $pat:term | $p:term })
      else
        throw ()
  | `($_ fun ($x:ident : $ty:term) â†¦ match $y:ident with | $pat => $p) =>
      if x == y then
        `({ $pat:term : $ty:term | $p:term })
      else
        throw ()
  | _ => throw ()

/-- The universal set on a type `Î±` is the set containing all elements of `Î±`.

This is conceptually the "same as" `Î±` (in set theory, it is actually the same), but type theory
makes the distinction that `Î±` is a type while `Set.univ` is a term of type `Set Î±`. `Set.univ` can
itself be coerced to a type `â†¥Set.univ` which is in bijection with (but distinct from) `Î±`. -/
def univ : Set Î± := {_a | True}
#align set.univ Set.univ

/-- `Set.insert a s` is the set `{a} âˆª s`.

Note that you should **not** use this definition directly, but instead write `insert a s` (which is
mediated by the `Insert` typeclass). -/
protected def insert (a : Î±) (s : Set Î±) : Set Î± := {b | b = a âˆ¨ b âˆˆ s}

instance : Insert Î± (Set Î±) := âŸ¨Set.insertâŸ©

/-- The singleton of an element `a` is the set with `a` as a single element.

Note that you should **not** use this definition directly, but instead write `{a}`. -/
protected def singleton (a : Î±) : Set Î± := {b | b = a}

instance instSingletonSet : Singleton Î± (Set Î±) := âŸ¨Set.singletonâŸ©

/-- The union of two sets `s` and `t` is the set of elements contained in either `s` or `t`.

Note that you should **not** use this definition directly, but instead write `s âˆª t`. -/
protected def union (sâ‚ sâ‚‚ : Set Î±) : Set Î± := {a | a âˆˆ sâ‚ âˆ¨ a âˆˆ sâ‚‚}

instance : Union (Set Î±) := âŸ¨Set.unionâŸ©

/-- The intersection of two sets `s` and `t` is the set of elements contained in both `s` and `t`.

Note that you should **not** use this definition directly, but instead write `s âˆ© t`. -/
protected def inter (sâ‚ sâ‚‚ : Set Î±) : Set Î± := {a | a âˆˆ sâ‚ âˆ§ a âˆˆ sâ‚‚}

instance : Inter (Set Î±) := âŸ¨Set.interâŸ©

/-- The complement of a set `s` is the set of elements not contained in `s`.

Note that you should **not** use this definition directly, but instead write `sá¶œ`. -/
protected def compl (s : Set Î±) : Set Î± := {a | a âˆ‰ s}

/-- The difference of two sets `s` and `t` is the set of elements contained in `s` but not in `t`.

Note that you should **not** use this definition directly, but instead write `s \ t`. -/
protected def diff (s t : Set Î±) : Set Î± := {a âˆˆ s | a âˆ‰ t}

instance : SDiff (Set Î±) := âŸ¨Set.diffâŸ©

/-- `ð’« s` is the set of all subsets of `s`. -/
def powerset (s : Set Î±) : Set (Set Î±) := {t | t âŠ† s}

@[inherit_doc] prefix:100 "ð’«" => powerset

/-- The image of `s : Set Î±` by `f : Î± â†’ Î²`, written `f '' s`, is the set of `b : Î²` such that
`f a = b` for some `a âˆˆ s`. -/
def image (f : Î± â†’ Î²) (s : Set Î±) : Set Î² := {f a | a âˆˆ s}

instance : Functor Set where map := @Set.image

instance : LawfulFunctor Set where
  id_map _ := congrArg ofPred <| funext fun _ â†¦ propext âŸ¨fun âŸ¨_, sb, rflâŸ© â†¦ sb, fun sb â†¦ âŸ¨_, sb, rflâŸ©âŸ©
  comp_map g h _ := congrArg ofPred <| funext <| fun c â†¦ propext
    âŸ¨fun âŸ¨a, âŸ¨hâ‚, hâ‚‚âŸ©âŸ© â†¦ âŸ¨g a, âŸ¨âŸ¨a, âŸ¨hâ‚, rflâŸ©âŸ©, hâ‚‚âŸ©âŸ©,
     fun âŸ¨_, âŸ¨âŸ¨a, âŸ¨hâ‚, hâ‚‚âŸ©âŸ©, hâ‚ƒâŸ©âŸ© â†¦ âŸ¨a, âŸ¨hâ‚, show h (g a) = c from hâ‚‚ â–¸ hâ‚ƒâŸ©âŸ©âŸ©
  map_const := rfl

/-- The property `s.Nonempty` expresses the fact that the set `s` is not empty. It should be used
in theorem assumptions instead of `âˆƒ x, x âˆˆ s` or `s â‰  âˆ…` as it gives access to a nice API thanks
to the dot notation. -/
protected def Nonempty (s : Set Î±) : Prop :=
  âˆƒ x, x âˆˆ s
#align set.nonempty Set.Nonempty

end Set
