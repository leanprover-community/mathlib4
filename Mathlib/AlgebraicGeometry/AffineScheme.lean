/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Cover.Open
import Mathlib.AlgebraicGeometry.GammaSpecAdjunction
import Mathlib.AlgebraicGeometry.Restrict
import Mathlib.CategoryTheory.Limits.Opposites
import Mathlib.RingTheory.Localization.InvSubmonoid
import Mathlib.RingTheory.RingHom.Surjective
import Mathlib.Topology.Sheaves.CommRingCat

/-!
# Affine schemes

We define the category of `AffineScheme`s as the essential image of `Spec`.
We also define predicates about affine schemes and affine open sets.

## Main definitions

* `AlgebraicGeometry.AffineScheme`: The category of affine schemes.
* `AlgebraicGeometry.IsAffine`: A scheme is affine if the canonical map `X ‚ü∂ Spec Œì(X)` is an
  isomorphism.
* `AlgebraicGeometry.Scheme.isoSpec`: The canonical isomorphism `X ‚âÖ Spec Œì(X)` for an affine
  scheme.
* `AlgebraicGeometry.AffineScheme.equivCommRingCat`: The equivalence of categories
  `AffineScheme ‚âå CommRing·µí·µñ` given by `AffineScheme.Spec : CommRing·µí·µñ ‚•§ AffineScheme` and
  `AffineScheme.Œì : AffineScheme·µí·µñ ‚•§ CommRingCat`.
* `AlgebraicGeometry.IsAffineOpen`: An open subset of a scheme is affine if the open subscheme is
  affine.
* `AlgebraicGeometry.IsAffineOpen.fromSpec`: The immersion `Spec ùí™‚Çì(U) ‚ü∂ X` for an affine `U`.

-/

-- Explicit universe annotations were used in this file to improve performance https://github.com/leanprover-community/mathlib4/issues/12737

noncomputable section

open CategoryTheory CategoryTheory.Limits Opposite TopologicalSpace

universe u

namespace AlgebraicGeometry

open Spec (structureSheaf)

/-- The category of affine schemes -/
-- Porting note (https://github.com/leanprover-community/mathlib4/issues/5171): linter not ported yet
-- @[nolint has_nonempty_instance]
def AffineScheme :=
  Scheme.Spec.EssImageSubcategory
deriving Category

/-- A Scheme is affine if the canonical map `X ‚ü∂ Spec Œì(X)` is an isomorphism. -/
class IsAffine (X : Scheme) : Prop where
  affine : IsIso X.toSpecŒì

attribute [instance] IsAffine.affine

instance (X : Scheme.{u}) [IsAffine X] : IsIso (ŒìSpec.adjunction.unit.app X) := @IsAffine.affine X _

/-- The canonical isomorphism `X ‚âÖ Spec Œì(X)` for an affine scheme. -/
@[simps! (config := .lemmasOnly) hom]
def Scheme.isoSpec (X : Scheme) [IsAffine X] : X ‚âÖ Spec Œì(X, ‚ä§) :=
  asIso X.toSpecŒì

@[reassoc]
theorem Scheme.isoSpec_hom_naturality {X Y : Scheme} [IsAffine X] [IsAffine Y] (f : X ‚ü∂ Y) :
    X.isoSpec.hom ‚â´ Spec.map (f.appTop) = f ‚â´ Y.isoSpec.hom := by
  simp only [isoSpec, asIso_hom, Scheme.toSpecŒì_naturality]

@[reassoc]
theorem Scheme.isoSpec_inv_naturality {X Y : Scheme} [IsAffine X] [IsAffine Y] (f : X ‚ü∂ Y) :
    Spec.map (f.appTop) ‚â´ Y.isoSpec.inv = X.isoSpec.inv ‚â´ f := by
  rw [Iso.eq_inv_comp, isoSpec, asIso_hom, ‚Üê Scheme.toSpecŒì_naturality_assoc, isoSpec,
    asIso_inv, IsIso.hom_inv_id, Category.comp_id]

/-- Construct an affine scheme from a scheme and the information that it is affine.
Also see `AffineScheme.of` for a typeclass version. -/
@[simps]
def AffineScheme.mk (X : Scheme) (_ : IsAffine X) : AffineScheme :=
  ‚ü®X, ŒìSpec.adjunction.mem_essImage_of_unit_isIso _‚ü©

/-- Construct an affine scheme from a scheme. Also see `AffineScheme.mk` for a non-typeclass
version. -/
def AffineScheme.of (X : Scheme) [h : IsAffine X] : AffineScheme :=
  AffineScheme.mk X h

/-- Type check a morphism of schemes as a morphism in `AffineScheme`. -/
def AffineScheme.ofHom {X Y : Scheme} [IsAffine X] [IsAffine Y] (f : X ‚ü∂ Y) :
    AffineScheme.of X ‚ü∂ AffineScheme.of Y :=
  f

theorem mem_Spec_essImage (X : Scheme) : X ‚àà Scheme.Spec.essImage ‚Üî IsAffine X :=
  ‚ü®fun h => ‚ü®Functor.essImage.unit_isIso h‚ü©,
    fun _ => ŒìSpec.adjunction.mem_essImage_of_unit_isIso _‚ü©

instance isAffine_affineScheme (X : AffineScheme.{u}) : IsAffine X.obj :=
  ‚ü®Functor.essImage.unit_isIso X.property‚ü©

instance (R : CommRingCat·µí·µñ) : IsAffine (Scheme.Spec.obj R) :=
  AlgebraicGeometry.isAffine_affineScheme ‚ü®_, Scheme.Spec.obj_mem_essImage R‚ü©

instance isAffine_Spec (R : CommRingCat) : IsAffine (Spec R) :=
  AlgebraicGeometry.isAffine_affineScheme ‚ü®_, Scheme.Spec.obj_mem_essImage (op R)‚ü©

theorem isAffine_of_isIso {X Y : Scheme} (f : X ‚ü∂ Y) [IsIso f] [h : IsAffine Y] : IsAffine X := by
  rw [‚Üê mem_Spec_essImage] at h ‚ä¢; exact Functor.essImage.ofIso (asIso f).symm h

/-- If `f : X ‚ü∂ Y` is a morphism between affine schemes, the corresponding arrow is isomorphic
to the arrow of the morphism on prime spectra induced by the map on global sections. -/
noncomputable
def arrowIsoSpecŒìOfIsAffine {X Y : Scheme} [IsAffine X] [IsAffine Y] (f : X ‚ü∂ Y) :
    Arrow.mk f ‚âÖ Arrow.mk (Spec.map f.appTop) :=
  Arrow.isoMk X.isoSpec Y.isoSpec (ŒìSpec.adjunction.unit_naturality _)

/-- If `f : A ‚ü∂ B` is a ring homomorphism, the corresponding arrow is isomorphic
to the arrow of the morphism induced on global sections by the map on prime spectra. -/
def arrowIsoŒìSpecOfIsAffine {A B : CommRingCat} (f : A ‚ü∂ B) :
    Arrow.mk f ‚âÖ Arrow.mk ((Spec.map f).appTop) :=
  Arrow.isoMk (Scheme.ŒìSpecIso _).symm (Scheme.ŒìSpecIso _).symm
    (Scheme.ŒìSpecIso_inv_naturality f).symm

theorem Scheme.isoSpec_Spec (R : CommRingCat.{u}) :
    (Spec R).isoSpec = Scheme.Spec.mapIso (Scheme.ŒìSpecIso R).op :=
  Iso.ext (SpecMap_ŒìSpecIso_hom R).symm

@[simp] theorem Scheme.isoSpec_Spec_hom (R : CommRingCat.{u}) :
    (Spec R).isoSpec.hom = Spec.map (Scheme.ŒìSpecIso R).hom :=
  (SpecMap_ŒìSpecIso_hom R).symm

@[simp] theorem Scheme.isoSpec_Spec_inv (R : CommRingCat.{u}) :
    (Spec R).isoSpec.inv = Spec.map (Scheme.ŒìSpecIso R).inv :=
  congr($(isoSpec_Spec R).inv)

lemma ext_of_isAffine {X Y : Scheme} [IsAffine Y] {f g : X ‚ü∂ Y} (e : f.appTop = g.appTop) :
    f = g := by
  rw [‚Üê cancel_mono Y.toSpecŒì, Scheme.toSpecŒì_naturality, Scheme.toSpecŒì_naturality, e]

namespace AffineScheme

/-- The `Spec` functor into the category of affine schemes. -/
def Spec : CommRingCat·µí·µñ ‚•§ AffineScheme :=
  Scheme.Spec.toEssImage

-- Porting note (https://github.com/leanprover-community/mathlib4/issues/11081): cannot automatically derive
instance Spec_full : Spec.Full := Functor.Full.toEssImage _

-- Porting note (https://github.com/leanprover-community/mathlib4/issues/11081): cannot automatically derive
instance Spec_faithful : Spec.Faithful := Functor.Faithful.toEssImage _

-- Porting note (https://github.com/leanprover-community/mathlib4/issues/11081): cannot automatically derive
instance Spec_essSurj : Spec.EssSurj := Functor.EssSurj.toEssImage (F := _)

/-- The forgetful functor `AffineScheme ‚•§ Scheme`. -/
@[simps!]
def forgetToScheme : AffineScheme ‚•§ Scheme :=
  Scheme.Spec.essImageInclusion

-- Porting note (https://github.com/leanprover-community/mathlib4/issues/11081): cannot automatically derive
instance forgetToScheme_full : forgetToScheme.Full :=
show (Scheme.Spec.essImageInclusion).Full from inferInstance

-- Porting note (https://github.com/leanprover-community/mathlib4/issues/11081): cannot automatically derive
instance forgetToScheme_faithful : forgetToScheme.Faithful :=
show (Scheme.Spec.essImageInclusion).Faithful from inferInstance

/-- The global section functor of an affine scheme. -/
def Œì : AffineScheme·µí·µñ ‚•§ CommRingCat :=
  forgetToScheme.op ‚ãô Scheme.Œì

/-- The category of affine schemes is equivalent to the category of commutative rings. -/
def equivCommRingCat : AffineScheme ‚âå CommRingCat·µí·µñ :=
  equivEssImageOfReflective.symm

instance : Œì.{u}.rightOp.IsEquivalence := equivCommRingCat.isEquivalence_functor

instance : Œì.{u}.rightOp.op.IsEquivalence := equivCommRingCat.op.isEquivalence_functor

instance ŒìIsEquiv : Œì.{u}.IsEquivalence :=
  inferInstanceAs (Œì.{u}.rightOp.op ‚ãô (opOpEquivalence _).functor).IsEquivalence

instance hasColimits : HasColimits AffineScheme.{u} :=
  haveI := Adjunction.has_limits_of_equivalence.{u} Œì.{u}
  Adjunction.has_colimits_of_equivalence.{u} (opOpEquivalence AffineScheme.{u}).inverse

instance hasLimits : HasLimits AffineScheme.{u} := by
  haveI := Adjunction.has_colimits_of_equivalence Œì.{u}
  haveI : HasLimits AffineScheme.{u}·µí·µñ·µí·µñ := Limits.hasLimits_op_of_hasColimits
  exact Adjunction.has_limits_of_equivalence (opOpEquivalence AffineScheme.{u}).inverse

noncomputable instance Œì_preservesLimits : PreservesLimits Œì.{u}.rightOp := inferInstance

noncomputable instance forgetToScheme_preservesLimits : PreservesLimits forgetToScheme := by
  apply (config := { allowSynthFailures := true })
    @preservesLimits_of_natIso _ _ _ _ _ _
      (isoWhiskerRight equivCommRingCat.unitIso forgetToScheme).symm
  change PreservesLimits (equivCommRingCat.functor ‚ãô Scheme.Spec)
  infer_instance

end AffineScheme

/-- An open subset of a scheme is affine if the open subscheme is affine. -/
def IsAffineOpen {X : Scheme} (U : X.Opens) : Prop :=
  IsAffine U

/-- The set of affine opens as a subset of `opens X`. -/
def Scheme.affineOpens (X : Scheme) : Set X.Opens :=
  {U : X.Opens | IsAffineOpen U}

instance {Y : Scheme.{u}} (U : Y.affineOpens) : IsAffine U :=
  U.property

theorem isAffineOpen_opensRange {X Y : Scheme} [IsAffine X] (f : X ‚ü∂ Y)
    [H : IsOpenImmersion f] : IsAffineOpen (Scheme.Hom.opensRange f) := by
  refine isAffine_of_isIso (IsOpenImmersion.isoOfRangeEq f (Y.ofRestrict _) ?_).inv
  exact Subtype.range_val.symm

theorem isAffineOpen_top (X : Scheme) [IsAffine X] : IsAffineOpen (‚ä§ : X.Opens) := by
  convert isAffineOpen_opensRange (ùüô X)
  ext1
  exact Set.range_id.symm

instance Scheme.isAffine_affineCover (X : Scheme) (i : X.affineCover.J) :
    IsAffine (X.affineCover.obj i) :=
  isAffine_Spec _

instance Scheme.isAffine_affineBasisCover (X : Scheme) (i : X.affineBasisCover.J) :
    IsAffine (X.affineBasisCover.obj i) :=
  isAffine_Spec _

instance Scheme.isAffine_affineOpenCover (X : Scheme) (ùí∞ : X.AffineOpenCover) (i : ùí∞.J) :
    IsAffine (ùí∞.openCover.obj i) :=
  inferInstanceAs (IsAffine (Spec (ùí∞.obj i)))

instance {X} [IsAffine X] (i) :
    IsAffine ((Scheme.coverOfIsIso (P := @IsOpenImmersion) (ùüô X)).obj i) := by
  dsimp; infer_instance

theorem isBasis_affine_open (X : Scheme) : Opens.IsBasis X.affineOpens := by
  rw [Opens.isBasis_iff_nbhd]
  rintro U x (hU : x ‚àà (U : Set X))
  obtain ‚ü®S, hS, hxS, hSU‚ü© := X.affineBasisCover_is_basis.exists_subset_of_mem_open hU U.isOpen
  refine ‚ü®‚ü®S, X.affineBasisCover_is_basis.isOpen hS‚ü©, ?_, hxS, hSU‚ü©
  rcases hS with ‚ü®i, rfl‚ü©
  exact isAffineOpen_opensRange _

theorem iSup_affineOpens_eq_top (X : Scheme) : ‚®Ü i : X.affineOpens, (i : X.Opens) = ‚ä§ := by
  apply Opens.ext
  rw [Opens.coe_iSup]
  apply IsTopologicalBasis.sUnion_eq
  rw [‚Üê Set.image_eq_range]
  exact isBasis_affine_open X

theorem Scheme.map_PrimeSpectrum_basicOpen_of_affine
    (X : Scheme) [IsAffine X] (f : Œì(X, ‚ä§)) :
    X.isoSpec.hom ‚Åª¬π·µÅ PrimeSpectrum.basicOpen f = X.basicOpen f :=
  Scheme.toSpecŒì_preimage_basicOpen _ _

theorem isBasis_basicOpen (X : Scheme) [IsAffine X] :
    Opens.IsBasis (Set.range (X.basicOpen : Œì(X, ‚ä§) ‚Üí X.Opens)) := by
  delta Opens.IsBasis
  convert PrimeSpectrum.isBasis_basic_opens.isInducing
    (TopCat.homeoOfIso (Scheme.forgetToTop.mapIso X.isoSpec)).isInducing using 1
  ext
  simp only [Set.mem_image, exists_exists_eq_and]
  constructor
  ¬∑ rintro ‚ü®_, ‚ü®x, rfl‚ü©, rfl‚ü©
    refine ‚ü®_, ‚ü®_, ‚ü®x, rfl‚ü©, rfl‚ü©, ?_‚ü©
    exact congr_arg Opens.carrier (Scheme.toSpecŒì_preimage_basicOpen _ _)
  ¬∑ rintro ‚ü®_, ‚ü®_, ‚ü®x, rfl‚ü©, rfl‚ü©, rfl‚ü©
    refine ‚ü®_, ‚ü®x, rfl‚ü©, ?_‚ü©
    exact congr_arg Opens.carrier (Scheme.toSpecŒì_preimage_basicOpen _ _).symm

/-- The canonical map `U ‚ü∂ Spec Œì(X, U)` for an open `U ‚äÜ X`. -/
noncomputable
def Scheme.Opens.toSpecŒì {X : Scheme.{u}} (U : X.Opens) :
    U.toScheme ‚ü∂ Spec Œì(X, U) :=
  U.toScheme.toSpecŒì ‚â´ Spec.map U.topIso.inv

@[reassoc (attr := simp)]
lemma Scheme.Opens.toSpecŒì_SpecMap_map {X : Scheme} (U V : X.Opens) (h : U ‚â§ V) :
    U.toSpecŒì ‚â´ Spec.map (X.presheaf.map (homOfLE h).op) = X.homOfLE h ‚â´ V.toSpecŒì := by
  delta Scheme.Opens.toSpecŒì
  simp [‚Üê Spec.map_comp, ‚Üê X.presheaf.map_comp]

@[simp]
lemma Scheme.Opens.toSpecŒì_top {X : Scheme} :
    (‚ä§ : X.Opens).toSpecŒì = (‚ä§ : X.Opens).Œπ ‚â´ X.toSpecŒì := by
  simp [Scheme.Opens.toSpecŒì]; rfl

namespace IsAffineOpen

variable {X Y : Scheme.{u}} {U : X.Opens} (hU : IsAffineOpen U) (f : Œì(X, U))

attribute [-simp] eqToHom_op in
/-- The isomorphism `U ‚âÖ Spec Œì(X, U)` for an affine `U`. -/
@[simps! (config := .lemmasOnly) inv]
def isoSpec :
    ‚ÜëU ‚âÖ Spec Œì(X, U) :=
  haveI : IsAffine U := hU
  U.toScheme.isoSpec ‚â™‚â´ Scheme.Spec.mapIso U.topIso.symm.op

lemma isoSpec_hom {X : Scheme.{u}} {U : X.Opens} (hU : IsAffineOpen U) :
    hU.isoSpec.hom = U.toSpecŒì := rfl

open IsLocalRing in
lemma isoSpec_hom_base_apply (x : U) :
    hU.isoSpec.hom.base x = (Spec.map (X.presheaf.germ U x x.2)).base (closedPoint _) := by
  dsimp [IsAffineOpen.isoSpec_hom, Scheme.isoSpec_hom, Scheme.toSpecŒì_base, Scheme.Opens.toSpecŒì]
  rw [‚Üê Scheme.comp_base_apply, ‚Üê Spec.map_comp,
    (Iso.eq_comp_inv _).mpr (Scheme.Opens.germ_stalkIso_hom U (V := ‚ä§) x trivial),
    X.presheaf.germ_res_assoc, Spec.map_comp, Scheme.comp_base_apply]
  congr 1
  exact IsLocalRing.comap_closedPoint (U.stalkIso x).inv.hom

lemma isoSpec_inv_appTop :
    hU.isoSpec.inv.appTop = U.topIso.hom ‚â´ (Scheme.ŒìSpecIso Œì(X, U)).inv := by
  simp only [Scheme.Opens.toScheme_presheaf_obj, isoSpec_inv, Scheme.isoSpec, asIso_inv,
    Scheme.comp_coeBase, Opens.map_comp_obj, Opens.map_top, Scheme.comp_app, Scheme.inv_appTop,
    Scheme.Opens.topIso_hom, Scheme.ŒìSpecIso_inv_naturality, IsIso.inv_comp_eq]
  rw [Scheme.toSpecŒì_appTop]
  erw [Iso.hom_inv_id_assoc]

lemma isoSpec_hom_appTop :
    hU.isoSpec.hom.appTop = (Scheme.ŒìSpecIso Œì(X, U)).hom ‚â´ U.topIso.inv := by
  have := congr(inv $hU.isoSpec_inv_appTop)
  rw [IsIso.inv_comp, IsIso.Iso.inv_inv, IsIso.Iso.inv_hom] at this
  have := (Scheme.Œì.map_inv hU.isoSpec.inv.op).trans this
  rwa [‚Üê op_inv, IsIso.Iso.inv_inv] at this

@[deprecated (since := "2024-11-16")] alias isoSpec_inv_app_top := isoSpec_inv_appTop
@[deprecated (since := "2024-11-16")] alias isoSpec_hom_app_top := isoSpec_hom_appTop

/-- The open immersion `Spec Œì(X, U) ‚ü∂ X` for an affine `U`. -/
def fromSpec :
    Spec Œì(X, U) ‚ü∂ X :=
  haveI : IsAffine U := hU
  hU.isoSpec.inv ‚â´ U.Œπ

instance isOpenImmersion_fromSpec :
    IsOpenImmersion hU.fromSpec := by
  delta fromSpec
  infer_instance

@[reassoc (attr := simp)]
lemma isoSpec_inv_Œπ : hU.isoSpec.inv ‚â´ U.Œπ = hU.fromSpec := rfl

@[simp]
theorem range_fromSpec :
    Set.range hU.fromSpec.base = (U : Set X) := by
  delta IsAffineOpen.fromSpec; dsimp [IsAffineOpen.isoSpec_inv]
  rw [Set.range_comp, Set.range_eq_univ.mpr, Set.image_univ]
  ¬∑ exact Subtype.range_coe
  rw [‚Üê coe_comp, ‚Üê TopCat.epi_iff_surjective]
  infer_instance

@[simp]
theorem opensRange_fromSpec :hU.fromSpec.opensRange = U := Opens.ext (range_fromSpec hU)

@[reassoc (attr := simp)]
theorem map_fromSpec {V : X.Opens} (hV : IsAffineOpen V) (f : op U ‚ü∂ op V) :
    Spec.map (X.presheaf.map f) ‚â´ hU.fromSpec = hV.fromSpec := by
  have : IsAffine U := hU
  haveI : IsAffine _ := hV
  conv_rhs =>
    rw [fromSpec, ‚Üê X.homOfLE_Œπ (V := U) f.unop.le, isoSpec_inv, Category.assoc,
      ‚Üê Scheme.isoSpec_inv_naturality_assoc,
      ‚Üê Spec.map_comp_assoc, Scheme.homOfLE_appTop, ‚Üê Functor.map_comp]
  rw [fromSpec, isoSpec_inv, Category.assoc, ‚Üê Spec.map_comp_assoc, ‚Üê Functor.map_comp]
  rfl

@[reassoc]
lemma Spec_map_appLE_fromSpec (f : X ‚ü∂ Y) {V : X.Opens} {U : Y.Opens}
    (hU : IsAffineOpen U) (hV : IsAffineOpen V) (i : V ‚â§ f ‚Åª¬π·µÅ U) :
    Spec.map (f.appLE U V i) ‚â´ hU.fromSpec = hV.fromSpec ‚â´ f := by
  have : IsAffine U := hU
  simp only [IsAffineOpen.fromSpec, Category.assoc, isoSpec_inv]
  simp_rw [‚Üê Scheme.homOfLE_Œπ _ i]
  rw [Category.assoc, ‚Üê morphismRestrict_Œπ,
    ‚Üê Category.assoc _ (f ‚à£_ U) U.Œπ, ‚Üê @Scheme.isoSpec_inv_naturality_assoc,
    ‚Üê Spec.map_comp_assoc, ‚Üê Spec.map_comp_assoc, Scheme.comp_appTop, morphismRestrict_appTop,
    Scheme.homOfLE_appTop, Scheme.Hom.app_eq_appLE, Scheme.Hom.appLE_map,
    Scheme.Hom.appLE_map, Scheme.Hom.appLE_map, Scheme.Hom.map_appLE]

lemma fromSpec_top [IsAffine X] : (isAffineOpen_top X).fromSpec = X.isoSpec.inv := by
  rw [fromSpec, isoSpec_inv, Category.assoc, ‚Üê @Scheme.isoSpec_inv_naturality,
    ‚Üê Spec.map_comp_assoc, Scheme.Opens.Œπ_appTop, ‚Üê X.presheaf.map_comp, ‚Üê op_comp,
    eqToHom_comp_homOfLE, ‚Üê eqToHom_eq_homOfLE rfl, eqToHom_refl, op_id, X.presheaf.map_id,
    Spec.map_id, Category.id_comp]

lemma fromSpec_app_of_le (V : X.Opens) (h : U ‚â§ V) :
    hU.fromSpec.app V = X.presheaf.map (homOfLE h).op ‚â´
      (Scheme.ŒìSpecIso Œì(X, U)).inv ‚â´ (Spec _).presheaf.map (homOfLE le_top).op := by
  have : U.Œπ ‚Åª¬π·µÅ V = ‚ä§ := eq_top_iff.mpr fun x _ ‚Ü¶ h x.2
  rw [IsAffineOpen.fromSpec, Scheme.comp_app, Scheme.Opens.Œπ_app, Scheme.app_eq _ this,
    ‚Üê Scheme.Hom.appTop, IsAffineOpen.isoSpec_inv_appTop]
  simp only [Scheme.Opens.toScheme_presheaf_map, Scheme.Opens.topIso_hom,
    Category.assoc, ‚Üê X.presheaf.map_comp_assoc]
  rfl

include hU in
protected theorem isCompact :
    IsCompact (U : Set X) := by
  convert @IsCompact.image _ _ _ _ Set.univ hU.fromSpec.base PrimeSpectrum.compactSpace.1
    (by fun_prop)
  convert hU.range_fromSpec.symm
  exact Set.image_univ

include hU in
theorem image_of_isOpenImmersion (f : X ‚ü∂ Y) [H : IsOpenImmersion f] :
    IsAffineOpen (f ''·µÅ U) := by
  have : IsAffine _ := hU
  convert isAffineOpen_opensRange (U.Œπ ‚â´ f)
  ext1
  exact Set.image_eq_range _ _

theorem preimage_of_isIso {U : Y.Opens} (hU : IsAffineOpen U) (f : X ‚ü∂ Y) [IsIso f] :
    IsAffineOpen (f ‚Åª¬π·µÅ U) :=
  haveI : IsAffine _ := hU
  isAffine_of_isIso (f ‚à£_ U)

theorem _root_.AlgebraicGeometry.Scheme.Hom.isAffineOpen_iff_of_isOpenImmersion
    (f : AlgebraicGeometry.Scheme.Hom X Y) [H : IsOpenImmersion f] {U : X.Opens} :
    IsAffineOpen (f ''·µÅ U) ‚Üî IsAffineOpen U := by
  refine ‚ü®fun hU => @isAffine_of_isIso _ _
    (IsOpenImmersion.isoOfRangeEq (X.ofRestrict U.isOpenEmbedding ‚â´ f) (Y.ofRestrict _) ?_).hom
      ?_ hU, fun hU => hU.image_of_isOpenImmersion f‚ü©
  ¬∑ rw [Scheme.comp_base, coe_comp, Set.range_comp]
    dsimp [Opens.coe_inclusion', Scheme.restrict]
    erw [Subtype.range_coe, Subtype.range_coe] -- now `erw` after https://github.com/leanprover-community/mathlib4/pull/13170
    rfl
  ¬∑ infer_instance

/-- The affine open sets of an open subscheme corresponds to
the affine open sets containing in the image. -/
@[simps]
def _root_.AlgebraicGeometry.IsOpenImmersion.affineOpensEquiv (f : X ‚ü∂ Y) [H : IsOpenImmersion f] :
    X.affineOpens ‚âÉ { U : Y.affineOpens // U ‚â§ f.opensRange } where
  toFun U := ‚ü®‚ü®f ''·µÅ U, U.2.image_of_isOpenImmersion f‚ü©, Set.image_subset_range _ _‚ü©
  invFun U := ‚ü®f ‚Åª¬π·µÅ U, f.isAffineOpen_iff_of_isOpenImmersion.mp (by
    rw [show f ''·µÅ f ‚Åª¬π·µÅ U = U from Opens.ext (Set.image_preimage_eq_of_subset U.2)]; exact U.1.2)‚ü©
  left_inv _ := Subtype.ext (Opens.ext (Set.preimage_image_eq _ H.base_open.injective))
  right_inv U := Subtype.ext (Subtype.ext (Opens.ext (Set.image_preimage_eq_of_subset U.2)))

/-- The affine open sets of an open subscheme
corresponds to the affine open sets containing in the subset. -/
@[simps! apply_coe_coe]
def _root_.AlgebraicGeometry.affineOpensRestrict {X : Scheme.{u}} (U : X.Opens) :
    U.toScheme.affineOpens ‚âÉ { V : X.affineOpens // V ‚â§ U } :=
  (IsOpenImmersion.affineOpensEquiv U.Œπ).trans (Equiv.subtypeEquivProp (by simp))

@[simp]
lemma _root_.AlgebraicGeometry.affineOpensRestrict_symm_apply_coe
    {X : Scheme.{u}} (U : X.Opens) (V) :
    ((affineOpensRestrict U).symm V).1 = U.Œπ ‚Åª¬π·µÅ V := rfl

instance (priority := 100) _root_.AlgebraicGeometry.Scheme.compactSpace_of_isAffine
    (X : Scheme) [IsAffine X] :
    CompactSpace X :=
  ‚ü®(isAffineOpen_top X).isCompact‚ü©

@[simp]
theorem fromSpec_preimage_self :
    hU.fromSpec ‚Åª¬π·µÅ U = ‚ä§ := by
  ext1
  rw [Opens.map_coe, Opens.coe_top, ‚Üê hU.range_fromSpec, ‚Üê Set.image_univ]
  exact Set.preimage_image_eq _ PresheafedSpace.IsOpenImmersion.base_open.injective

theorem ŒìSpecIso_hom_fromSpec_app :
    (Scheme.ŒìSpecIso Œì(X, U)).hom ‚â´ hU.fromSpec.app U =
      (Spec Œì(X, U)).presheaf.map (eqToHom hU.fromSpec_preimage_self).op := by
  simp only [fromSpec, Scheme.comp_coeBase, Opens.map_comp_obj, Scheme.comp_app,
    Scheme.Opens.Œπ_app_self, eqToHom_op, Scheme.app_eq _ U.Œπ_preimage_self,
    Scheme.Opens.toScheme_presheaf_map, eqToHom_unop, eqToHom_map U.Œπ.opensFunctor, Opens.map_top,
    isoSpec_inv_appTop, Scheme.Opens.topIso_hom, Category.assoc, ‚Üê Functor.map_comp_assoc,
    eqToHom_trans, eqToHom_refl, X.presheaf.map_id, Category.id_comp, Iso.hom_inv_id_assoc]

@[elementwise]
theorem fromSpec_app_self :
    hU.fromSpec.app U = (Scheme.ŒìSpecIso Œì(X, U)).inv ‚â´
      (Spec Œì(X, U)).presheaf.map (eqToHom hU.fromSpec_preimage_self).op := by
  rw [‚Üê hU.ŒìSpecIso_hom_fromSpec_app, Iso.inv_hom_id_assoc]

theorem fromSpec_preimage_basicOpen' :
    hU.fromSpec ‚Åª¬π·µÅ X.basicOpen f = (Spec Œì(X, U)).basicOpen ((Scheme.ŒìSpecIso Œì(X, U)).inv f) := by
  rw [Scheme.preimage_basicOpen, hU.fromSpec_app_self]
  exact Scheme.basicOpen_res_eq _ _ (eqToHom hU.fromSpec_preimage_self).op

theorem fromSpec_preimage_basicOpen :
    hU.fromSpec ‚Åª¬π·µÅ X.basicOpen f = PrimeSpectrum.basicOpen f := by
  rw [fromSpec_preimage_basicOpen', ‚Üê basicOpen_eq_of_affine]

theorem fromSpec_image_basicOpen :
    hU.fromSpec ''·µÅ (PrimeSpectrum.basicOpen f) = X.basicOpen f := by
  rw [‚Üê hU.fromSpec_preimage_basicOpen]
  ext1
  change hU.fromSpec.base '' (hU.fromSpec.base ‚Åª¬π' (X.basicOpen f : Set X)) = _
  rw [Set.image_preimage_eq_inter_range, Set.inter_eq_left, hU.range_fromSpec]
  exact Scheme.basicOpen_le _ _

@[simp]
theorem basicOpen_fromSpec_app :
    (Spec Œì(X, U)).basicOpen (hU.fromSpec.app U f) = PrimeSpectrum.basicOpen f := by
  rw [‚Üê hU.fromSpec_preimage_basicOpen, Scheme.preimage_basicOpen]

include hU in
theorem basicOpen :
    IsAffineOpen (X.basicOpen f) := by
  rw [‚Üê hU.fromSpec_image_basicOpen, Scheme.Hom.isAffineOpen_iff_of_isOpenImmersion]
  convert isAffineOpen_opensRange
    (Spec.map (CommRingCat.ofHom <| algebraMap Œì(X, U) (Localization.Away f)))
  exact Opens.ext (PrimeSpectrum.localization_away_comap_range (Localization.Away f) f).symm

lemma Spec_basicOpen {R : CommRingCat} (f : R) :
    IsAffineOpen (X := Spec R) (PrimeSpectrum.basicOpen f) :=
  basicOpen_eq_of_affine f ‚ñ∏ (isAffineOpen_top (Spec (.of R))).basicOpen _

instance [IsAffine X] (r : Œì(X, ‚ä§)) : IsAffine (X.basicOpen r) :=
  (isAffineOpen_top X).basicOpen _

include hU in
theorem Œπ_basicOpen_preimage (r : Œì(X, ‚ä§)) :
    IsAffineOpen ((X.basicOpen r).Œπ ‚Åª¬π·µÅ U) := by
  apply (X.basicOpen r).Œπ.isAffineOpen_iff_of_isOpenImmersion.mp
  dsimp [Scheme.Hom.opensFunctor, LocallyRingedSpace.IsOpenImmersion.opensFunctor]
  rw [Opens.functor_obj_map_obj, Opens.isOpenEmbedding_obj_top, inf_comm,
    ‚Üê Scheme.basicOpen_res _ _ (homOfLE le_top).op]
  exact hU.basicOpen _

include hU in
theorem exists_basicOpen_le {V : X.Opens} (x : V) (h : ‚Üëx ‚àà U) :
    ‚àÉ f : Œì(X, U), X.basicOpen f ‚â§ V ‚àß ‚Üëx ‚àà X.basicOpen f := by
  have : IsAffine _ := hU
  obtain ‚ü®_, ‚ü®_, ‚ü®r, rfl‚ü©, rfl‚ü©, h‚ÇÅ, h‚ÇÇ‚ü© :=
    (isBasis_basicOpen U).exists_subset_of_mem_open (x.2 : (‚ü®x, h‚ü© : U) ‚àà _)
      ((Opens.map U.inclusion').obj V).isOpen
  have :
    U.Œπ ''·µÅ (U.toScheme.basicOpen r) =
      X.basicOpen (X.presheaf.map (eqToHom U.isOpenEmbedding_obj_top.symm).op r) := by
    refine (Scheme.image_basicOpen U.Œπ r).trans ?_
    rw [Scheme.basicOpen_res_eq]
    simp only [Scheme.Opens.toScheme_presheaf_obj, Scheme.Opens.Œπ_appIso, Iso.refl_inv,
      CommRingCat.id_apply]
  use X.presheaf.map (eqToHom U.isOpenEmbedding_obj_top.symm).op r
  rw [‚Üê this]
  exact ‚ü®Set.image_subset_iff.mpr h‚ÇÇ, ‚ü®_, h‚ü©, h‚ÇÅ, rfl‚ü©

noncomputable
instance {R : CommRingCat} {U} : Algebra R Œì(Spec R, U) :=
  ((Scheme.ŒìSpecIso R).inv ‚â´ (Spec R).presheaf.map (homOfLE le_top).op).hom.toAlgebra

@[simp]
lemma algebraMap_Spec_obj {R : CommRingCat} {U} : algebraMap R Œì(Spec R, U) =
    ((Scheme.ŒìSpecIso R).inv ‚â´ (Spec R).presheaf.map (homOfLE le_top).op).hom := rfl

instance {R : CommRingCat} {f : R} :
    IsLocalization.Away f Œì(Spec R, PrimeSpectrum.basicOpen f) :=
  inferInstanceAs (IsLocalization.Away f
    ((Spec.structureSheaf R).val.obj (op <| PrimeSpectrum.basicOpen f)))

/-- Given an affine open U and some `f : U`,
this is the canonical map `Œì(ùí™‚Çì, D(f)) ‚ü∂ Œì(Spec ùí™‚Çì(U), D(f))`
This is an isomorphism, as witnessed by an `IsIso` instance. -/
def basicOpenSectionsToAffine :
    Œì(X, X.basicOpen f) ‚ü∂ Œì(Spec Œì(X, U), PrimeSpectrum.basicOpen f) :=
  hU.fromSpec.c.app (op <| X.basicOpen f) ‚â´
    (Spec Œì(X, U)).presheaf.map (eqToHom <| (hU.fromSpec_preimage_basicOpen f).symm).op

instance basicOpenSectionsToAffine_isIso :
    IsIso (basicOpenSectionsToAffine hU f) := by
  delta basicOpenSectionsToAffine
  refine IsIso.comp_isIso' ?_ inferInstance
  apply PresheafedSpace.IsOpenImmersion.isIso_of_subset
  rw [hU.range_fromSpec]
  exact RingedSpace.basicOpen_le _ _

include hU in
theorem isLocalization_basicOpen :
    IsLocalization.Away f Œì(X, X.basicOpen f) := by
  apply
    (IsLocalization.isLocalization_iff_of_ringEquiv (Submonoid.powers f)
      (asIso <| basicOpenSectionsToAffine hU f).commRingCatIsoToRingEquiv).mpr
  convert StructureSheaf.IsLocalization.to_basicOpen _ f using 1
  -- Porting note: more hand holding is required here, the next 4 lines were not necessary
  delta StructureSheaf.openAlgebra
  congr 1
  rw [RingEquiv.toRingHom_eq_coe, CategoryTheory.Iso.commRingCatIsoToRingEquiv_toRingHom, asIso_hom]
  dsimp [CommRingCat.ofHom, RingHom.algebraMap_toAlgebra]
  change (X.presheaf.map _ ‚â´ basicOpenSectionsToAffine hU f).hom = _
  delta basicOpenSectionsToAffine
  rw [hU.fromSpec.naturality_assoc, hU.fromSpec_app_self]
  simp only [Category.assoc, ‚Üê Functor.map_comp, ‚Üê op_comp]
  exact CommRingCat.hom_ext_iff.mp (StructureSheaf.toOpen_res _ _ _ _)

instance _root_.AlgebraicGeometry.isLocalization_away_of_isAffine
    [IsAffine X] (r : Œì(X, ‚ä§)) :
    IsLocalization.Away r Œì(X, X.basicOpen r) :=
  isLocalization_basicOpen (isAffineOpen_top X) r

lemma appLE_eq_away_map {X Y : Scheme.{u}} (f : X ‚ü∂ Y) {U : Y.Opens} (hU : IsAffineOpen U)
    {V : X.Opens} (hV : IsAffineOpen V) (e) (r : Œì(Y, U)) :
    letI := hU.isLocalization_basicOpen r
    letI := hV.isLocalization_basicOpen (f.appLE U V e r)
    f.appLE (Y.basicOpen r) (X.basicOpen (f.appLE U V e r)) (by simp [Scheme.Hom.appLE]) =
        CommRingCat.ofHom (IsLocalization.Away.map _ _ (f.appLE U V e).hom r) := by
  letI := hU.isLocalization_basicOpen r
  letI := hV.isLocalization_basicOpen (f.appLE U V e r)
  ext : 1
  apply IsLocalization.ringHom_ext (.powers r)
  rw [IsLocalization.Away.map, IsLocalization.map_comp,
    RingHom.algebraMap_toAlgebra, RingHom.algebraMap_toAlgebra, ‚Üê CommRingCat.hom_comp,
    ‚Üê CommRingCat.hom_comp, Scheme.Hom.appLE_map, Scheme.Hom.map_appLE]

lemma app_basicOpen_eq_away_map {X Y : Scheme.{u}} (f : X ‚ü∂ Y) {U : Y.Opens}
    (hU : IsAffineOpen U) (h : IsAffineOpen (f ‚Åª¬π·µÅ U)) (r : Œì(Y, U)) :
    haveI := hU.isLocalization_basicOpen r
    haveI := h.isLocalization_basicOpen (f.app U r)
    f.app (Y.basicOpen r) =
      (CommRingCat.ofHom
        (IsLocalization.Away.map Œì(Y, Y.basicOpen r) Œì(X, X.basicOpen (f.app U r)) (f.app U).hom r)
        ‚â´ X.presheaf.map (eqToHom (by simp)).op) := by
  haveI := hU.isLocalization_basicOpen r
  haveI := h.isLocalization_basicOpen (f.app U r)
  ext : 1
  apply IsLocalization.ringHom_ext (.powers r)
  rw [IsLocalization.Away.map, CommRingCat.hom_comp, RingHom.comp_assoc,
    IsLocalization.map_comp, RingHom.algebraMap_toAlgebra,
    RingHom.algebraMap_toAlgebra, ‚Üê RingHom.comp_assoc, ‚Üê CommRingCat.hom_comp,
    ‚Üê CommRingCat.hom_comp, ‚Üê X.presheaf.map_comp]
  simp

/-- `f.app (Y.basicOpen r)` is isomorphic to map induced on localizations
`Œì(Y, Y.basicOpen r) ‚ü∂ Œì(X, X.basicOpen (f.app U r))` -/
def appBasicOpenIsoAwayMap {X Y : Scheme.{u}} (f : X ‚ü∂ Y) {U : Y.Opens}
    (hU : IsAffineOpen U) (h : IsAffineOpen (f ‚Åª¬π·µÅ U)) (r : Œì(Y, U)) :
    haveI := hU.isLocalization_basicOpen r
    haveI := h.isLocalization_basicOpen (f.app U r)
    Arrow.mk (f.app (Y.basicOpen r)) ‚âÖ
      Arrow.mk (CommRingCat.ofHom (IsLocalization.Away.map Œì(Y, Y.basicOpen r)
        Œì(X, X.basicOpen (f.app U r)) (f.app U).hom r)) :=
  Arrow.isoMk (Iso.refl _) (X.presheaf.mapIso (eqToIso (by simp)).op) <| by
    simp [hU.app_basicOpen_eq_away_map f h]

include hU in
theorem isLocalization_of_eq_basicOpen {V : X.Opens} (i : V ‚ü∂ U) (e : V = X.basicOpen f) :
    @IsLocalization.Away _ _ f Œì(X, V) _ (X.presheaf.map i.op).hom.toAlgebra := by
  subst e; convert isLocalization_basicOpen hU f using 3

instance _root_.AlgebraicGeometry.Œì_restrict_isLocalization
    (X : Scheme.{u}) [IsAffine X] (r : Œì(X, ‚ä§)) :
    IsLocalization.Away r Œì(X.basicOpen r, ‚ä§) :=
  (isAffineOpen_top X).isLocalization_of_eq_basicOpen r _ (Opens.isOpenEmbedding_obj_top _)

include hU in
theorem basicOpen_basicOpen_is_basicOpen (g : Œì(X, X.basicOpen f)) :
    ‚àÉ f' : Œì(X, U), X.basicOpen f' = X.basicOpen g := by
  have := isLocalization_basicOpen hU f
  obtain ‚ü®x, ‚ü®_, n, rfl‚ü©, rfl‚ü© := IsLocalization.surj'' (Submonoid.powers f) g
  use f * x
  rw [Algebra.smul_def, Scheme.basicOpen_mul, Scheme.basicOpen_mul, RingHom.algebraMap_toAlgebra]
  rw [Scheme.basicOpen_res]
  refine (inf_eq_left.mpr ?_).symm
  -- Porting note: a little help is needed here
  convert inf_le_left (Œ± := X.Opens) using 1
  apply Scheme.basicOpen_of_isUnit
  apply
    Submonoid.leftInv_le_isUnit _
      (IsLocalization.toInvSubmonoid (Submonoid.powers f) (Œì(X, X.basicOpen f))
        _).prop

include hU in
theorem _root_.AlgebraicGeometry.exists_basicOpen_le_affine_inter
    {V : X.Opens} (hV : IsAffineOpen V) (x : X) (hx : x ‚àà U ‚äì V) :
    ‚àÉ (f : Œì(X, U)) (g : Œì(X, V)), X.basicOpen f = X.basicOpen g ‚àß x ‚àà X.basicOpen f := by
  obtain ‚ü®f, hf‚ÇÅ, hf‚ÇÇ‚ü© := hU.exists_basicOpen_le ‚ü®x, hx.2‚ü© hx.1
  obtain ‚ü®g, hg‚ÇÅ, hg‚ÇÇ‚ü© := hV.exists_basicOpen_le ‚ü®x, hf‚ÇÇ‚ü© hx.2
  obtain ‚ü®f', hf'‚ü© :=
    basicOpen_basicOpen_is_basicOpen hU f (X.presheaf.map (homOfLE hf‚ÇÅ : _ ‚ü∂ V).op g)
  replace hf' := (hf'.trans (RingedSpace.basicOpen_res _ _ _)).trans (inf_eq_right.mpr hg‚ÇÅ)
  exact ‚ü®f', g, hf', hf'.symm ‚ñ∏ hg‚ÇÇ‚ü©

/-- The prime ideal of `ùí™‚Çì(U)` corresponding to a point `x : U`. -/
noncomputable def primeIdealOf (x : U) :
    PrimeSpectrum Œì(X, U) :=
  hU.isoSpec.hom.base x

theorem fromSpec_primeIdealOf (x : U) :
    hU.fromSpec.base (hU.primeIdealOf x) = x.1 := by
  dsimp only [IsAffineOpen.fromSpec, Subtype.coe_mk, IsAffineOpen.primeIdealOf]
  rw [‚Üê Scheme.comp_base_apply, Iso.hom_inv_id_assoc]
  rfl

open IsLocalRing in
theorem primeIdealOf_eq_map_closedPoint (x : U) :
    hU.primeIdealOf x = (Spec.map (X.presheaf.germ _ x x.2)).base (closedPoint _) :=
  hU.isoSpec_hom_base_apply _

theorem isLocalization_stalk' (y : PrimeSpectrum Œì(X, U)) (hy : hU.fromSpec.base y ‚àà U) :
    @IsLocalization.AtPrime
      (R := Œì(X, U))
      (S := X.presheaf.stalk <| hU.fromSpec.base y) _ _
      ((TopCat.Presheaf.algebra_section_stalk X.presheaf _)) y.asIdeal _ := by
  apply
    (@IsLocalization.isLocalization_iff_of_ringEquiv (R := Œì(X, U))
      (S := X.presheaf.stalk (hU.fromSpec.base y)) _ y.asIdeal.primeCompl _
      (TopCat.Presheaf.algebra_section_stalk X.presheaf ‚ü®hU.fromSpec.base y, hy‚ü©) _ _
      (asIso <| hU.fromSpec.stalkMap y).commRingCatIsoToRingEquiv).mpr
  -- Porting note: need to know what the ring is and after convert, instead of equality
  -- we get an `iff`.
  convert StructureSheaf.IsLocalization.to_stalk Œì(X, U) y using 1
  delta IsLocalization.AtPrime StructureSheaf.stalkAlgebra
  rw [iff_iff_eq]
  congr 2
  rw [RingHom.algebraMap_toAlgebra, RingEquiv.toRingHom_eq_coe,
    CategoryTheory.Iso.commRingCatIsoToRingEquiv_toRingHom, asIso_hom, ‚Üê CommRingCat.hom_comp,
    Scheme.stalkMap_germ, IsAffineOpen.fromSpec_app_self, Category.assoc, TopCat.Presheaf.germ_res]
  rfl

-- Porting note: I have split this into two lemmas
theorem isLocalization_stalk (x : U) :
    IsLocalization.AtPrime (X.presheaf.stalk x) (hU.primeIdealOf x).asIdeal := by
  rcases x with ‚ü®x, hx‚ü©
  set y := hU.primeIdealOf ‚ü®x, hx‚ü© with hy
  have : hU.fromSpec.base y = x := hy ‚ñ∏ hU.fromSpec_primeIdealOf ‚ü®x, hx‚ü©
  clear_value y
  subst this
  exact hU.isLocalization_stalk' y hx

lemma stalkMap_injective (f : X ‚ü∂ Y) {U : Opens Y} (hU : IsAffineOpen U) (x : X)
    (hx : f.base x ‚àà U)
    (h : ‚àÄ g, f.stalkMap x (Y.presheaf.germ U (f.base x) hx g) = 0 ‚Üí
      Y.presheaf.germ U (f.base x) hx g = 0) :
    Function.Injective (f.stalkMap x) := by
  letI := Y.presheaf.algebra_section_stalk ‚ü®f.base x, hx‚ü©
  apply (hU.isLocalization_stalk ‚ü®f.base x, hx‚ü©).injective_of_map_algebraMap_zero
  exact h

/-- The basic open set of a section `f` on an affine open as an `X.affineOpens`. -/
@[simps]
def _root_.AlgebraicGeometry.Scheme.affineBasicOpen
    (X : Scheme) {U : X.affineOpens} (f : Œì(X, U)) : X.affineOpens :=
  ‚ü®X.basicOpen f, U.prop.basicOpen f‚ü©

include hU in
/--
In an affine open set `U`, a family of basic open covers `U` iff the sections span `Œì(X, U)`.
See `iSup_basicOpen_of_span_eq_top` for the inverse direction without the affine-ness assumption.
-/
theorem basicOpen_union_eq_self_iff (s : Set Œì(X, U)) :
    ‚®Ü f : s, X.basicOpen (f : Œì(X, U)) = U ‚Üî Ideal.span s = ‚ä§ := by
  trans ‚ãÉ i : s, (PrimeSpectrum.basicOpen i.1).1 = Set.univ
  ¬∑ trans
      hU.fromSpec.base ‚Åª¬π' (‚®Ü f : s, X.basicOpen (f : Œì(X, U))).1 =
        hU.fromSpec.base ‚Åª¬π' U.1
    ¬∑ refine ‚ü®fun h => by rw [h], ?_‚ü©
      intro h
      apply_fun Set.image hU.fromSpec.base at h
      rw [Set.image_preimage_eq_inter_range, Set.image_preimage_eq_inter_range, hU.range_fromSpec]
        at h
      simp only [Set.inter_self, Opens.carrier_eq_coe, Set.inter_eq_right] at h
      ext1
      refine Set.Subset.antisymm ?_ h
      simp only [Set.iUnion_subset_iff, SetCoe.forall, Opens.coe_iSup]
      intro x _
      exact X.basicOpen_le x
    ¬∑ simp only [Opens.iSup_def, Subtype.coe_mk, Set.preimage_iUnion]
      congr! 1
      ¬∑ refine congr_arg (Set.iUnion ¬∑) ?_
        ext1 x
        exact congr_arg Opens.carrier (hU.fromSpec_preimage_basicOpen _)
      ¬∑ exact congr_arg Opens.carrier hU.fromSpec_preimage_self
  ¬∑ simp only [Opens.carrier_eq_coe, PrimeSpectrum.basicOpen_eq_zeroLocus_compl]
    rw [‚Üê Set.compl_iInter, Set.compl_univ_iff, ‚Üê PrimeSpectrum.zeroLocus_iUnion, ‚Üê
      PrimeSpectrum.zeroLocus_empty_iff_eq_top, PrimeSpectrum.zeroLocus_span]
    simp only [Set.iUnion_singleton_eq_range, Subtype.range_val_subtype, Set.setOf_mem_eq]

include hU in
theorem self_le_basicOpen_union_iff (s : Set Œì(X, U)) :
    (U ‚â§ ‚®Ü f : s, X.basicOpen f.1) ‚Üî Ideal.span s = ‚ä§ := by
  rw [‚Üê hU.basicOpen_union_eq_self_iff, @comm _ Eq]
  refine ‚ü®fun h => le_antisymm h ?_, le_of_eq‚ü©
  simp only [iSup_le_iff, SetCoe.forall]
  intro x _
  exact X.basicOpen_le x

end IsAffineOpen

open _root_.PrimeSpectrum in
/-- The restriction of `Spec.map f` to a basic open `D(r)` is isomorphic to `Spec.map` of the
localization of `f` away from `r`. -/
noncomputable
def SpecMapRestrictBasicOpenIso {R S : CommRingCat} (f : R ‚ü∂ S) (r : R) :
    Arrow.mk (Spec.map f ‚à£_ (PrimeSpectrum.basicOpen r)) ‚âÖ
      Arrow.mk (Spec.map <| CommRingCat.ofHom (Localization.awayMap f.hom r)) := by
  letI e‚ÇÅ : Localization.Away r ‚âÉ‚Çê[R] Œì(Spec R, basicOpen r) :=
    IsLocalization.algEquiv (Submonoid.powers r) _ _
  letI e‚ÇÇ : Localization.Away (f r) ‚âÉ‚Çê[S] Œì(Spec S, basicOpen (f r)) :=
    IsLocalization.algEquiv (Submonoid.powers (f r)) _ _
  refine Arrow.isoMk ?_ ?_ ?_
  ¬∑ exact (Spec (.of S)).isoOfEq (comap_basicOpen _ _) ‚â™‚â´
      (IsAffineOpen.Spec_basicOpen (f r)).isoSpec ‚â™‚â´ Scheme.Spec.mapIso e‚ÇÇ.toCommRingCatIso.op
  ¬∑ exact (IsAffineOpen.Spec_basicOpen r).isoSpec ‚â™‚â´ Scheme.Spec.mapIso e‚ÇÅ.toCommRingCatIso.op
  ¬∑ have := AlgebraicGeometry.IsOpenImmersion.of_isLocalization
      (S := (Localization.Away r)) r
    rw [‚Üê cancel_mono (Spec.map (CommRingCat.ofHom (algebraMap R (Localization.Away r))))]
    simp only [Arrow.mk_left, Arrow.mk_right, Functor.id_obj, Scheme.isoOfEq_rfl, Iso.refl_trans,
      Iso.trans_hom, Functor.mapIso_hom, Iso.op_hom, Iso.symm_hom,
      Scheme.Spec_map, Quiver.Hom.unop_op, Arrow.mk_hom, Category.assoc,
      ‚Üê Spec.map_comp]
    show _ ‚â´ Spec.map (CommRingCat.ofHom
        ((e‚ÇÇ.toRingHom.comp (Localization.awayMap f.hom r)).comp (algebraMap R _)))
      = _ ‚â´ _ ‚â´ Spec.map (CommRingCat.ofHom (e‚ÇÅ.toRingHom.comp (algebraMap R _)))
    rw [RingHom.comp_assoc]
    conv => enter [1,2,1,1,2]; tactic => exact IsLocalization.map_comp _
    rw [‚Üê RingHom.comp_assoc]
    conv => enter [1,2,1,1,1]; tactic => exact e‚ÇÇ.toAlgHom.comp_algebraMap
    conv => enter [2,2,2,1,1]; tactic => exact e‚ÇÅ.toAlgHom.comp_algebraMap
    show _ ‚â´ Spec.map (f ‚â´ (Scheme.ŒìSpecIso S).inv ‚â´
      (Spec S).presheaf.map (homOfLE le_top).op) =
      Spec.map f ‚à£_ basicOpen r ‚â´ _ ‚â´ Spec.map ((Scheme.ŒìSpecIso R).inv ‚â´
      (Spec R).presheaf.map (homOfLE le_top).op)
    simp only [IsAffineOpen.isoSpec_hom, homOfLE_leOfHom, Spec.map_comp, Category.assoc,
      Scheme.Opens.toSpecŒì_SpecMap_map_assoc, Scheme.Opens.toSpecŒì_top, Scheme.homOfLE_Œπ_assoc,
      morphismRestrict_Œπ_assoc]
    simp only [‚Üê SpecMap_ŒìSpecIso_hom, ‚Üê Spec.map_comp, Category.assoc, Iso.inv_hom_id,
      Category.comp_id, Category.id_comp]
    rfl

lemma stalkMap_injective_of_isAffine {X Y : Scheme} (f : X ‚ü∂ Y) [IsAffine Y] (x : X)
    (h : ‚àÄ g, f.stalkMap x (Y.presheaf.Œìgerm (f.base x) g) = 0 ‚Üí
      Y.presheaf.Œìgerm (f.base x) g = 0) :
    Function.Injective (f.stalkMap x) :=
  (isAffineOpen_top Y).stalkMap_injective f x trivial h

/--
Given a spanning set of `Œì(X, U)`, the corresponding basic open sets cover `U`.
See `IsAffineOpen.basicOpen_union_eq_self_iff` for the inverse direction for affine open sets.
-/
lemma iSup_basicOpen_of_span_eq_top {X : Scheme} (U) (s : Set Œì(X, U))
    (hs : Ideal.span s = ‚ä§) : (‚®Ü i ‚àà s, X.basicOpen i) = U := by
  apply le_antisymm
  ¬∑ rw [iSup‚ÇÇ_le_iff]
    exact fun i _ ‚Ü¶ X.basicOpen_le i
  ¬∑ intro x hx
    obtain ‚ü®_, ‚ü®V, hV, rfl‚ü©, hxV, hVU‚ü© := (isBasis_affine_open X).exists_subset_of_mem_open hx U.2
    refine SetLike.mem_of_subset ?_ hxV
    rw [‚Üê (hV.basicOpen_union_eq_self_iff (X.presheaf.map (homOfLE hVU).op '' s)).mpr
      (by rw [‚Üê Ideal.map_span, hs, Ideal.map_top])]
    simp only [Opens.iSup_mk, Opens.carrier_eq_coe, Set.iUnion_coe_set, Set.mem_image,
      Set.iUnion_exists, Set.biUnion_and', Set.iUnion_iUnion_eq_right, Scheme.basicOpen_res,
      Opens.coe_inf, Opens.coe_mk, Set.iUnion_subset_iff]
    exact fun i hi ‚Ü¶ (Set.inter_subset_right.trans
      (Set.subset_iUnion‚ÇÇ (s := fun x _ ‚Ü¶ (X.basicOpen x : Set X)) i hi))

/-- Let `P` be a predicate on the affine open sets of `X` satisfying
1. If `P` holds on `U`, then `P` holds on the basic open set of every section on `U`.
2. If `P` holds for a family of basic open sets covering `U`, then `P` holds for `U`.
3. There exists an affine open cover of `X` each satisfying `P`.

Then `P` holds for every affine open of `X`.

This is also known as the **Affine communication lemma** in [*The rising sea*][RisingSea]. -/
@[elab_as_elim]
theorem of_affine_open_cover {X : Scheme} {P : X.affineOpens ‚Üí Prop}
    {Œπ} (U : Œπ ‚Üí X.affineOpens) (iSup_U : (‚®Ü i, U i : X.Opens) = ‚ä§)
    (V : X.affineOpens)
    (basicOpen : ‚àÄ (U : X.affineOpens) (f : Œì(X, U)), P U ‚Üí P (X.affineBasicOpen f))
    (openCover :
      ‚àÄ (U : X.affineOpens) (s : Finset (Œì(X, U)))
        (_ : Ideal.span (s : Set (Œì(X, U))) = ‚ä§),
        (‚àÄ f : s, P (X.affineBasicOpen f.1)) ‚Üí P U)
    (hU : ‚àÄ i, P (U i)) : P V := by
  classical
  have : ‚àÄ (x : V.1), ‚àÉ f : Œì(X, V), ‚Üëx ‚àà X.basicOpen f ‚àß P (X.affineBasicOpen f) := by
    intro x
    obtain ‚ü®i, hi‚ü© := Opens.mem_iSup.mp (show x.1 ‚àà (‚®Ü i, U i : X.Opens) from iSup_U ‚ñ∏ trivial)
    obtain ‚ü®f, g, e, hf‚ü© := exists_basicOpen_le_affine_inter V.prop (U i).prop x ‚ü®x.prop, hi‚ü©
    refine ‚ü®f, hf, ?_‚ü©
    convert basicOpen _ g (hU i) using 1
    ext1
    exact e
  choose f hf‚ÇÅ hf‚ÇÇ using this
  suffices Ideal.span (Set.range f) = ‚ä§ by
    obtain ‚ü®t, ht‚ÇÅ, ht‚ÇÇ‚ü© := (Ideal.span_eq_top_iff_finite _).mp this
    apply openCover V t ht‚ÇÇ
    rintro ‚ü®i, hi‚ü©
    obtain ‚ü®x, rfl‚ü© := ht‚ÇÅ hi
    exact hf‚ÇÇ x
  rw [‚Üê V.prop.self_le_basicOpen_union_iff]
  intro x hx
  rw [iSup_range', SetLike.mem_coe, Opens.mem_iSup]
  exact ‚ü®_, hf‚ÇÅ ‚ü®x, hx‚ü©‚ü©

section ZeroLocus

/-- On a locally ringed space `X`, the preimage of the zero locus of the prime spectrum
of `Œì(X, ‚ä§)` under `toŒìSpecFun` agrees with the associated zero locus on `X`. -/
lemma Scheme.toŒìSpec_preimage_zeroLocus_eq {X : Scheme.{u}} (s : Set Œì(X, ‚ä§)) :
    X.toSpecŒì.base ‚Åª¬π' PrimeSpectrum.zeroLocus s = X.zeroLocus s :=
  LocallyRingedSpace.toŒìSpec_preimage_zeroLocus_eq s

open ConcreteCategory

/-- If `X` is affine, the image of the zero locus of global sections of `X` under `toŒìSpecFun`
is the zero locus in terms of the prime spectrum of `Œì(X, ‚ä§)`. -/
lemma Scheme.toŒìSpec_image_zeroLocus_eq_of_isAffine {X : Scheme.{u}} [IsAffine X]
    (s : Set Œì(X, ‚ä§)) :
    X.isoSpec.hom.base '' X.zeroLocus s = PrimeSpectrum.zeroLocus s := by
  erw [‚Üê X.toŒìSpec_preimage_zeroLocus_eq, Set.image_preimage_eq]
  exact (bijective_of_isIso X.isoSpec.hom.base).surjective

/-- If `X` is an affine scheme, every closed set of `X` is the zero locus
of a set of global sections. -/
lemma Scheme.eq_zeroLocus_of_isClosed_of_isAffine (X : Scheme.{u}) [IsAffine X] (s : Set X) :
    IsClosed s ‚Üî ‚àÉ I : Ideal (Œì(X, ‚ä§)), s = X.zeroLocus (I : Set Œì(X, ‚ä§)) := by
  refine ‚ü®fun hs ‚Ü¶ ?_, ?_‚ü©
  ¬∑ let Z : Set (Spec <| Œì(X, ‚ä§)) := X.toŒìSpecFun '' s
    have hZ : IsClosed Z := (X.isoSpec.hom.homeomorph).isClosedMap _ hs
    obtain ‚ü®I, (hI : Z = _)‚ü© := (PrimeSpectrum.isClosed_iff_zeroLocus_ideal _).mp hZ
    use I
    simp only [‚Üê Scheme.toŒìSpec_preimage_zeroLocus_eq, ‚Üê hI, Z]
    erw [Set.preimage_image_eq _ (bijective_of_isIso X.isoSpec.hom.base).injective]
  ¬∑ rintro ‚ü®I, rfl‚ü©
    exact zeroLocus_isClosed X I.carrier

end ZeroLocus

section Factorization

variable {X : Scheme.{u}} {A : CommRingCat}

/-- If `X ‚ü∂ Spec A` is a morphism of schemes, then `Spec` of `A ‚ß∏ specTargetImage f`
is the scheme-theoretic image of `f`. For this quotient as an object of `CommRingCat` see
`specTargetImage` below. -/
def specTargetImageIdeal (f : X ‚ü∂ Spec A) : Ideal A :=
  (RingHom.ker <| (((ŒìSpec.adjunction).homEquiv X (op A)).symm f).unop.hom)

/-- If `X ‚ü∂ Spec A` is a morphism of schemes, then `Spec` of `specTargetImage f` is the
scheme-theoretic image of `f` and `f` factors as
`specTargetImageFactorization f ‚â´ Spec.map (specTargetImageRingHom f)`
(see `specTargetImageFactorization_comp`). -/
def specTargetImage (f : X ‚ü∂ Spec A) : CommRingCat :=
  CommRingCat.of (A ‚ß∏ specTargetImageIdeal f)

/-- If `f : X ‚ü∂ Spec A` is a morphism of schemes, then `f` factors via
the inclusion of `Spec (specTargetImage f)` into `X`. -/
def specTargetImageFactorization (f : X ‚ü∂ Spec A) : X ‚ü∂ Spec (specTargetImage f) :=
  (ŒìSpec.adjunction).homEquiv X (op <| specTargetImage f) (Opposite.op
    (CommRingCat.ofHom (RingHom.kerLift _)))

/-- If `f : X ‚ü∂ Spec A` is a morphism of schemes, the induced morphism on spectra of
`specTargetImageRingHom f` is the inclusion of the scheme-theoretic image of `f` into `Spec A`. -/
def specTargetImageRingHom (f : X ‚ü∂ Spec A) : A ‚ü∂ specTargetImage f :=
  CommRingCat.ofHom (Ideal.Quotient.mk (specTargetImageIdeal f))

variable (f : X ‚ü∂ Spec A)

lemma specTargetImageRingHom_surjective : Function.Surjective (specTargetImageRingHom f) :=
  Ideal.Quotient.mk_surjective

lemma specTargetImageFactorization_app_injective :
    Function.Injective <| (specTargetImageFactorization f).appTop := by
  let œÜ : A ‚ü∂ Œì(X, ‚ä§) := (((ŒìSpec.adjunction).homEquiv X (op A)).symm f).unop
  let œÜ' : specTargetImage f ‚ü∂ Scheme.Œì.obj (op X) := CommRingCat.ofHom (RingHom.kerLift œÜ.hom)
  show Function.Injective <| ((ŒìSpec.adjunction.homEquiv X _) œÜ'.op).appTop
  rw [ŒìSpec_adjunction_homEquiv_eq]
  apply (RingHom.kerLift_injective œÜ.hom).comp
  exact ((ConcreteCategory.isIso_iff_bijective (Scheme.ŒìSpecIso _).hom).mp inferInstance).injective

@[reassoc (attr := simp)]
lemma specTargetImageFactorization_comp :
    specTargetImageFactorization f ‚â´ Spec.map (specTargetImageRingHom f) = f := by
  let œÜ : A ‚ü∂ Œì(X, ‚ä§) := (((ŒìSpec.adjunction).homEquiv X (op A)).symm f).unop
  let œÜ' : specTargetImage f ‚ü∂ Scheme.Œì.obj (op X) := CommRingCat.ofHom (RingHom.kerLift œÜ.hom)
  apply ((ŒìSpec.adjunction).homEquiv X (op A)).symm.injective
  apply Opposite.unop_injective
  rw [Adjunction.homEquiv_naturality_left_symm, Adjunction.homEquiv_counit]
  change (_ ‚â´ _) ‚â´ _ = œÜ
  erw [‚Üê Spec_Œì_naturality]
  rw [Category.assoc]
  erw [ŒìSpecIso_inv_ŒìSpec_adjunction_homEquiv œÜ']
  ext a
  apply RingHom.kerLift_mk

open RingHom

variable {Y : Scheme.{u}} [IsAffine Y] (f : X ‚ü∂ Y)

/-- The scheme-theoretic image of a morphism `f : X ‚ü∂ Y` with affine target.
`f` factors as `affineTargetImageFactorization f ‚â´ affineTargetImageInclusion f`
(see `affineTargetImageFactorization_comp`). -/
def affineTargetImage (f : X ‚ü∂ Y) : Scheme.{u} :=
  Spec <| specTargetImage (f ‚â´ Y.isoSpec.hom)

instance : IsAffine (affineTargetImage f) := inferInstanceAs <| IsAffine <| Spec _

/-- The inclusion of the scheme-theoretic image of a morphism with affine target. -/
def affineTargetImageInclusion (f : X ‚ü∂ Y) : affineTargetImage f ‚ü∂ Y :=
  Spec.map (specTargetImageRingHom (f ‚â´ Y.isoSpec.hom)) ‚â´ Y.isoSpec.inv

lemma affineTargetImageInclusion_app_surjective :
    Function.Surjective <| (affineTargetImageInclusion f).appTop := by
  simp only [Scheme.comp_coeBase, Opens.map_comp_obj, Opens.map_top, Scheme.comp_app,
    CommRingCat.hom_comp, affineTargetImageInclusion, RingHom.coe_comp]
  apply Function.Surjective.comp
  ¬∑ haveI : (toMorphismProperty (fun f ‚Ü¶ Function.Surjective f)).RespectsIso := by
      rw [‚Üê toMorphismProperty_respectsIso_iff]
      exact surjective_respectsIso
    exact (MorphismProperty.arrow_mk_iso_iff
      (toMorphismProperty (fun f ‚Ü¶ Function.Surjective f))
      (arrowIsoŒìSpecOfIsAffine (specTargetImageRingHom (f ‚â´ Y.isoSpec.hom))).symm).mpr <|
        specTargetImageRingHom_surjective (f ‚â´ Y.isoSpec.hom)
  ¬∑ apply Function.Bijective.surjective
    exact ConcreteCategory.bijective_of_isIso (Scheme.Hom.app Y.isoSpec.inv ‚ä§)

/-- The induced morphism from `X` to the scheme-theoretic image
of a morphism `f : X ‚ü∂ Y` with affine target. -/
def affineTargetImageFactorization (f : X ‚ü∂ Y) : X ‚ü∂ affineTargetImage f :=
  specTargetImageFactorization (f ‚â´ Y.isoSpec.hom)

lemma affineTargetImageFactorization_app_injective :
    Function.Injective <| (affineTargetImageFactorization f).appTop :=
  specTargetImageFactorization_app_injective (f ‚â´ Y.isoSpec.hom)

@[reassoc (attr := simp)]
lemma affineTargetImageFactorization_comp :
    affineTargetImageFactorization f ‚â´ affineTargetImageInclusion f = f := by
  simp [affineTargetImageFactorization, affineTargetImageInclusion]

end Factorization

section Stalks

/-- Variant of `AlgebraicGeometry.localRingHom_comp_stalkIso` for `Spec.map`. -/
@[elementwise]
lemma Scheme.localRingHom_comp_stalkIso {R S : CommRingCat.{u}} (f : R ‚ü∂ S) (p : PrimeSpectrum S) :
    (StructureSheaf.stalkIso R (PrimeSpectrum.comap f.hom p)).hom ‚â´
      (CommRingCat.ofHom <| Localization.localRingHom
        (PrimeSpectrum.comap f.hom p).asIdeal p.asIdeal f.hom rfl) ‚â´
      (StructureSheaf.stalkIso S p).inv = (Spec.map f).stalkMap p :=
  AlgebraicGeometry.localRingHom_comp_stalkIso f p

/-- Given a morphism of rings `f : R ‚ü∂ S`, the stalk map of `Spec S ‚ü∂ Spec R` at
a prime of `S` is isomorphic to the localized ring homomorphism. -/
def Scheme.arrowStalkMapSpecIso {R S : CommRingCat.{u}} (f : R ‚ü∂ S) (p : PrimeSpectrum S) :
    Arrow.mk ((Spec.map f).stalkMap p) ‚âÖ Arrow.mk (CommRingCat.ofHom <| Localization.localRingHom
      (PrimeSpectrum.comap f.hom p).asIdeal p.asIdeal f.hom rfl) := Arrow.isoMk
  (StructureSheaf.stalkIso R (PrimeSpectrum.comap f.hom p))
  (StructureSheaf.stalkIso S p) <| by
    rw [‚Üê Scheme.localRingHom_comp_stalkIso]
    simp

end Stalks

@[deprecated (since := "2024-06-21"), nolint defLemma]
alias isAffineAffineScheme := isAffine_affineScheme
@[deprecated (since := "2024-06-21"), nolint defLemma]
alias SpecIsAffine := isAffine_Spec
@[deprecated (since := "2024-06-21")]
alias isAffineOfIso := isAffine_of_isIso
@[deprecated (since := "2024-06-21")]
alias rangeIsAffineOpenOfOpenImmersion := isAffineOpen_opensRange
@[deprecated (since := "2024-06-21")]
alias topIsAffineOpen := isAffineOpen_top
@[deprecated (since := "2024-06-21"), nolint defLemma]
alias Scheme.affineCoverIsAffine := Scheme.isAffine_affineCover
@[deprecated (since := "2024-06-21"), nolint defLemma]
alias Scheme.affineBasisCoverIsAffine := Scheme.isAffine_affineBasisCover
@[deprecated (since := "2024-06-21")]
alias IsAffineOpen.fromSpec_range := IsAffineOpen.range_fromSpec
@[deprecated (since := "2024-06-21")]
alias IsAffineOpen.imageIsOpenImmersion := IsAffineOpen.image_of_isOpenImmersion
@[deprecated (since := "2024-06-21"), nolint defLemma]
alias Scheme.quasi_compact_of_affine := Scheme.compactSpace_of_isAffine
@[deprecated (since := "2024-06-21")]
alias IsAffineOpen.fromSpec_base_preimage := IsAffineOpen.fromSpec_preimage_self
@[deprecated (since := "2024-06-21")]
alias IsAffineOpen.fromSpec_map_basicOpen' := IsAffineOpen.fromSpec_preimage_basicOpen'
@[deprecated (since := "2024-06-21")]
alias IsAffineOpen.fromSpec_map_basicOpen := IsAffineOpen.fromSpec_preimage_basicOpen
@[deprecated (since := "2024-06-21")]
alias IsAffineOpen.opensFunctor_map_basicOpen := IsAffineOpen.fromSpec_image_basicOpen
@[deprecated (since := "2024-06-21")]
alias IsAffineOpen.basicOpenIsAffine := IsAffineOpen.basicOpen
@[deprecated (since := "2024-06-21")]
alias IsAffineOpen.mapRestrictBasicOpen := IsAffineOpen.Œπ_basicOpen_preimage

end AlgebraicGeometry
