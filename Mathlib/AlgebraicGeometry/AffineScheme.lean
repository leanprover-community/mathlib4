/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.GammaSpecAdjunction
import Mathlib.AlgebraicGeometry.Restrict
import Mathlib.AlgebraicGeometry.Cover.Open
import Mathlib.CategoryTheory.Limits.Opposites
import Mathlib.RingTheory.Localization.InvSubmonoid

/-!
# Affine schemes

We define the category of `AffineScheme`s as the essential image of `Spec`.
We also define predicates about affine schemes and affine open sets.

## Main definitions

* `AlgebraicGeometry.AffineScheme`: The category of affine schemes.
* `AlgebraicGeometry.IsAffine`: A scheme is affine if the canonical map `X ‚ü∂ Spec Œì(X)` is an
  isomorphism.
* `AlgebraicGeometry.Scheme.isoSpec`: The canonical isomorphism `X ‚âÖ Spec Œì(X)` for an affine
  scheme.
* `AlgebraicGeometry.AffineScheme.equivCommRingCat`: The equivalence of categories
  `AffineScheme ‚âå CommRing·µí·µñ` given by `AffineScheme.Spec : CommRing·µí·µñ ‚•§ AffineScheme` and
  `AffineScheme.Œì : AffineScheme·µí·µñ ‚•§ CommRingCat`.
* `AlgebraicGeometry.IsAffineOpen`: An open subset of a scheme is affine if the open subscheme is
  affine.
* `AlgebraicGeometry.IsAffineOpen.fromSpec`: The immersion `Spec ùí™‚Çì(U) ‚ü∂ X` for an affine `U`.

-/

-- Explicit universe annotations were used in this file to improve performance #12737

noncomputable section

open CategoryTheory CategoryTheory.Limits Opposite TopologicalSpace

universe u

namespace AlgebraicGeometry

open Spec (structureSheaf)

/-- The category of affine schemes -/
-- Porting note(#5171): linter not ported yet
-- @[nolint has_nonempty_instance]
def AffineScheme :=
  Scheme.Spec.EssImageSubcategory
deriving Category

/-- A Scheme is affine if the canonical map `X ‚ü∂ Spec Œì(X)` is an isomorphism. -/
class IsAffine (X : Scheme) : Prop where
  affine : IsIso (ŒìSpec.adjunction.unit.app X)

attribute [instance] IsAffine.affine

/-- The canonical isomorphism `X ‚âÖ Spec Œì(X)` for an affine scheme. -/
@[simps! (config := .lemmasOnly) hom]
def Scheme.isoSpec (X : Scheme) [IsAffine X] : X ‚âÖ Spec Œì(X, ‚ä§) :=
  asIso (ŒìSpec.adjunction.unit.app X)

@[reassoc]
theorem Scheme.isoSpec_hom_naturality {X Y : Scheme} [IsAffine X] [IsAffine Y] (f : X ‚ü∂ Y) :
    X.isoSpec.hom ‚â´ Spec.map (f.app ‚ä§) = f ‚â´ Y.isoSpec.hom := by
  simp only [isoSpec, asIso_hom, ŒìSpec.adjunction_unit_naturality]

@[reassoc]
theorem Scheme.isoSpec_inv_naturality {X Y : Scheme} [IsAffine X] [IsAffine Y] (f : X ‚ü∂ Y) :
    Spec.map (f.app ‚ä§) ‚â´ Y.isoSpec.inv = X.isoSpec.inv ‚â´ f := by
  rw [Iso.eq_inv_comp, isoSpec, asIso_hom, ‚Üê ŒìSpec.adjunction_unit_naturality_assoc, isoSpec,
    asIso_inv, IsIso.hom_inv_id, Category.comp_id]

/-- Construct an affine scheme from a scheme and the information that it is affine.
Also see `AffineScheme.of` for a typeclass version. -/
@[simps]
def AffineScheme.mk (X : Scheme) (_ : IsAffine X) : AffineScheme :=
  ‚ü®X, ŒìSpec.adjunction.mem_essImage_of_unit_isIso _‚ü©

/-- Construct an affine scheme from a scheme. Also see `AffineScheme.mk` for a non-typeclass
version. -/
def AffineScheme.of (X : Scheme) [h : IsAffine X] : AffineScheme :=
  AffineScheme.mk X h

/-- Type check a morphism of schemes as a morphism in `AffineScheme`. -/
def AffineScheme.ofHom {X Y : Scheme} [IsAffine X] [IsAffine Y] (f : X ‚ü∂ Y) :
    AffineScheme.of X ‚ü∂ AffineScheme.of Y :=
  f

theorem mem_Spec_essImage (X : Scheme) : X ‚àà Scheme.Spec.essImage ‚Üî IsAffine X :=
  ‚ü®fun h => ‚ü®Functor.essImage.unit_isIso h‚ü©,
    fun _ => ŒìSpec.adjunction.mem_essImage_of_unit_isIso _‚ü©

instance isAffine_affineScheme (X : AffineScheme.{u}) : IsAffine X.obj :=
  ‚ü®Functor.essImage.unit_isIso X.property‚ü©

instance (R : CommRingCat·µí·µñ) : IsAffine (Scheme.Spec.obj R) :=
  AlgebraicGeometry.isAffine_affineScheme ‚ü®_, Scheme.Spec.obj_mem_essImage R‚ü©

instance isAffine_Spec (R : CommRingCat) : IsAffine (Spec R) :=
  AlgebraicGeometry.isAffine_affineScheme ‚ü®_, Scheme.Spec.obj_mem_essImage (op R)‚ü©

theorem isAffine_of_isIso {X Y : Scheme} (f : X ‚ü∂ Y) [IsIso f] [h : IsAffine Y] : IsAffine X := by
  rw [‚Üê mem_Spec_essImage] at h ‚ä¢; exact Functor.essImage.ofIso (asIso f).symm h

/-- If `f : X ‚ü∂ Y` is a morphism between affine schemes, the corresponding arrow is isomorphic
to the arrow of the morphism on prime spectra induced by the map on global sections. -/
noncomputable
def arrowIsoSpecŒìOfIsAffine {X Y : Scheme} [IsAffine X] [IsAffine Y] (f : X ‚ü∂ Y) :
    Arrow.mk f ‚âÖ Arrow.mk (Spec.map (Scheme.Œì.map f.op)) :=
  Arrow.isoMk X.isoSpec Y.isoSpec (ŒìSpec.adjunction.unit_naturality _)

namespace AffineScheme

/-- The `Spec` functor into the category of affine schemes. -/
def Spec : CommRingCat·µí·µñ ‚•§ AffineScheme :=
  Scheme.Spec.toEssImage

-- Porting note (#11081): cannot automatically derive
instance Spec_full : Spec.Full := Functor.Full.toEssImage _

-- Porting note (#11081): cannot automatically derive
instance Spec_faithful : Spec.Faithful := Functor.Faithful.toEssImage _

-- Porting note (#11081): cannot automatically derive
instance Spec_essSurj : Spec.EssSurj := Functor.EssSurj.toEssImage (F := _)

/-- The forgetful functor `AffineScheme ‚•§ Scheme`. -/
@[simps!]
def forgetToScheme : AffineScheme ‚•§ Scheme :=
  Scheme.Spec.essImageInclusion

-- Porting note (#11081): cannot automatically derive
instance forgetToScheme_full : forgetToScheme.Full :=
show (Scheme.Spec.essImageInclusion).Full from inferInstance

-- Porting note (#11081): cannot automatically derive
instance forgetToScheme_faithful : forgetToScheme.Faithful :=
show (Scheme.Spec.essImageInclusion).Faithful from inferInstance

/-- The global section functor of an affine scheme. -/
def Œì : AffineScheme·µí·µñ ‚•§ CommRingCat :=
  forgetToScheme.op ‚ãô Scheme.Œì

/-- The category of affine schemes is equivalent to the category of commutative rings. -/
def equivCommRingCat : AffineScheme ‚âå CommRingCat·µí·µñ :=
  equivEssImageOfReflective.symm

instance : Œì.{u}.rightOp.IsEquivalence := equivCommRingCat.isEquivalence_functor

instance : Œì.{u}.rightOp.op.IsEquivalence := equivCommRingCat.op.isEquivalence_functor

instance ŒìIsEquiv : Œì.{u}.IsEquivalence :=
  inferInstanceAs (Œì.{u}.rightOp.op ‚ãô (opOpEquivalence _).functor).IsEquivalence

instance hasColimits : HasColimits AffineScheme.{u} :=
  haveI := Adjunction.has_limits_of_equivalence.{u} Œì.{u}
  Adjunction.has_colimits_of_equivalence.{u} (opOpEquivalence AffineScheme.{u}).inverse

instance hasLimits : HasLimits AffineScheme.{u} := by
  haveI := Adjunction.has_colimits_of_equivalence Œì.{u}
  haveI : HasLimits AffineScheme.{u}·µí·µñ·µí·µñ := Limits.hasLimits_op_of_hasColimits
  exact Adjunction.has_limits_of_equivalence (opOpEquivalence AffineScheme.{u}).inverse

noncomputable instance Œì_preservesLimits : PreservesLimits Œì.{u}.rightOp := inferInstance

noncomputable instance forgetToScheme_preservesLimits : PreservesLimits forgetToScheme := by
  apply (config := { allowSynthFailures := true })
    @preservesLimitsOfNatIso _ _ _ _ _ _
      (isoWhiskerRight equivCommRingCat.unitIso forgetToScheme).symm
  change PreservesLimits (equivCommRingCat.functor ‚ãô Scheme.Spec)
  infer_instance

end AffineScheme

/-- An open subset of a scheme is affine if the open subscheme is affine. -/
def IsAffineOpen {X : Scheme} (U : X.Opens) : Prop :=
  IsAffine U

/-- The set of affine opens as a subset of `opens X`. -/
def Scheme.affineOpens (X : Scheme) : Set X.Opens :=
  {U : X.Opens | IsAffineOpen U}

instance {Y : Scheme.{u}} (U : Y.affineOpens) : IsAffine U :=
  U.property

theorem isAffineOpen_opensRange {X Y : Scheme} [IsAffine X] (f : X ‚ü∂ Y)
    [H : IsOpenImmersion f] : IsAffineOpen (Scheme.Hom.opensRange f) := by
  refine isAffine_of_isIso (IsOpenImmersion.isoOfRangeEq f (Y.ofRestrict _) ?_).inv
  exact Subtype.range_val.symm

theorem isAffineOpen_top (X : Scheme) [IsAffine X] : IsAffineOpen (‚ä§ : X.Opens) := by
  convert isAffineOpen_opensRange (ùüô X)
  ext1
  exact Set.range_id.symm

instance Scheme.isAffine_affineCover (X : Scheme) (i : X.affineCover.J) :
    IsAffine (X.affineCover.obj i) :=
  isAffine_Spec _

instance Scheme.isAffine_affineBasisCover (X : Scheme) (i : X.affineBasisCover.J) :
    IsAffine (X.affineBasisCover.obj i) :=
  isAffine_Spec _

instance Scheme.isAffine_affineOpenCover (X : Scheme) (ùí∞ : X.AffineOpenCover) (i : ùí∞.J) :
    IsAffine (ùí∞.openCover.obj i) :=
  inferInstanceAs (IsAffine (Spec (ùí∞.obj i)))

instance {X} [IsAffine X] (i) : IsAffine ((Scheme.openCoverOfIsIso (ùüô X)).obj i) := by
  dsimp; infer_instance

theorem isBasis_affine_open (X : Scheme) : Opens.IsBasis X.affineOpens := by
  rw [Opens.isBasis_iff_nbhd]
  rintro U x (hU : x ‚àà (U : Set X))
  obtain ‚ü®S, hS, hxS, hSU‚ü© := X.affineBasisCover_is_basis.exists_subset_of_mem_open hU U.isOpen
  refine ‚ü®‚ü®S, X.affineBasisCover_is_basis.isOpen hS‚ü©, ?_, hxS, hSU‚ü©
  rcases hS with ‚ü®i, rfl‚ü©
  exact isAffineOpen_opensRange _

theorem iSup_affineOpens_eq_top (X : Scheme) : ‚®Ü i : X.affineOpens, (i : X.Opens) = ‚ä§ := by
  apply Opens.ext
  rw [Opens.coe_iSup]
  apply IsTopologicalBasis.sUnion_eq
  rw [‚Üê Set.image_eq_range]
  exact isBasis_affine_open X

theorem Scheme.map_PrimeSpectrum_basicOpen_of_affine
    (X : Scheme) [IsAffine X] (f : Scheme.Œì.obj (op X)) :
    X.isoSpec.hom ‚Åª¬π·µÅ PrimeSpectrum.basicOpen f = X.basicOpen f :=
  ŒìSpec.adjunction_unit_map_basicOpen _ _

theorem isBasis_basicOpen (X : Scheme) [IsAffine X] :
    Opens.IsBasis (Set.range (X.basicOpen : Œì(X, ‚ä§) ‚Üí X.Opens)) := by
  delta Opens.IsBasis
  convert PrimeSpectrum.isBasis_basic_opens.inducing
    (TopCat.homeoOfIso (Scheme.forgetToTop.mapIso X.isoSpec)).inducing using 1
  ext
  simp only [Set.mem_image, exists_exists_eq_and]
  constructor
  ¬∑ rintro ‚ü®_, ‚ü®x, rfl‚ü©, rfl‚ü©
    refine ‚ü®_, ‚ü®_, ‚ü®x, rfl‚ü©, rfl‚ü©, ?_‚ü©
    exact congr_arg Opens.carrier (ŒìSpec.adjunction_unit_map_basicOpen _ _)
  ¬∑ rintro ‚ü®_, ‚ü®_, ‚ü®x, rfl‚ü©, rfl‚ü©, rfl‚ü©
    refine ‚ü®_, ‚ü®x, rfl‚ü©, ?_‚ü©
    exact congr_arg Opens.carrier (ŒìSpec.adjunction_unit_map_basicOpen _ _).symm

namespace IsAffineOpen

variable {X Y : Scheme.{u}} {U : X.Opens} (hU : IsAffineOpen U) (f : Œì(X, U))

/-- The open immersion `Spec Œì(X, U) ‚ü∂ X` for an affine `U`. -/
def fromSpec :
    Spec Œì(X, U) ‚ü∂ X :=
  haveI : IsAffine U := hU
  Spec.map (X.presheaf.map (eqToHom U.openEmbedding_obj_top.symm).op) ‚â´
    U.toScheme.isoSpec.inv ‚â´ U.Œπ

instance isOpenImmersion_fromSpec :
    IsOpenImmersion hU.fromSpec := by
  delta fromSpec
  infer_instance

@[simp]
theorem range_fromSpec :
    Set.range hU.fromSpec.1.base = (U : Set X) := by
  delta IsAffineOpen.fromSpec; dsimp
  rw [Function.comp.assoc, Set.range_comp, Set.range_iff_surjective.mpr, Set.image_univ]
  ¬∑ exact Subtype.range_coe
  erw [‚Üê coe_comp, ‚Üê TopCat.epi_iff_surjective] -- now `erw` after #13170
  infer_instance

@[simp]
theorem opensRange_fromSpec : Scheme.Hom.opensRange hU.fromSpec = U := Opens.ext (range_fromSpec hU)

@[reassoc (attr := simp)]
theorem map_fromSpec {V : X.Opens} (hV : IsAffineOpen V) (f : op U ‚ü∂ op V) :
    Spec.map (X.presheaf.map f) ‚â´ hU.fromSpec = hV.fromSpec := by
  have : IsAffine (X.restrictFunctor.obj U).left := hU
  haveI : IsAffine _ := hV
  conv_rhs =>
    rw [fromSpec, ‚Üê X.restrictFunctor_map_ofRestrict f.unop, ‚Üê Scheme.isoSpec_inv_naturality_assoc,
      ‚Üê Spec.map_comp_assoc, Scheme.restrictFunctor_map_app, ‚Üê Functor.map_comp]
  rw [fromSpec, ‚Üê Spec.map_comp_assoc, ‚Üê Functor.map_comp]
  rfl

include hU in
protected theorem isCompact :
    IsCompact (U : Set X) := by
  convert @IsCompact.image _ _ _ _ Set.univ hU.fromSpec.1.base PrimeSpectrum.compactSpace.1
    (by fun_prop)
  convert hU.range_fromSpec.symm
  exact Set.image_univ

include hU in
theorem image_of_isOpenImmersion (f : X ‚ü∂ Y) [H : IsOpenImmersion f] :
    IsAffineOpen (f ''·µÅ U) := by
  have : IsAffine _ := hU
  convert isAffineOpen_opensRange (U.Œπ ‚â´ f)
  ext1
  exact Set.image_eq_range _ _

theorem preimage_of_isIso {U : Y.Opens} (hU : IsAffineOpen U) (f : X ‚ü∂ Y) [IsIso f] :
    IsAffineOpen (f ‚Åª¬π·µÅ U) :=
  haveI : IsAffine _ := hU
  isAffine_of_isIso (f ‚à£_ U)

theorem _root_.AlgebraicGeometry.Scheme.Hom.isAffineOpen_iff_of_isOpenImmersion
    (f : AlgebraicGeometry.Scheme.Hom X Y) [H : IsOpenImmersion f] {U : X.Opens} :
    IsAffineOpen (f ''·µÅ U) ‚Üî IsAffineOpen U := by
  refine ‚ü®fun hU => @isAffine_of_isIso _ _
    (IsOpenImmersion.isoOfRangeEq (X.ofRestrict U.openEmbedding ‚â´ f) (Y.ofRestrict _) ?_).hom ?_ hU,
    fun hU => hU.image_of_isOpenImmersion f‚ü©
  ¬∑ erw [Scheme.comp_val_base, coe_comp, Set.range_comp] -- now `erw` after #13170
    dsimp [Opens.coe_inclusion, Scheme.restrict]
    erw [Subtype.range_coe, Subtype.range_coe] -- now `erw` after #13170
    rfl
  ¬∑ infer_instance

/-- The affine open sets of an open subscheme corresponds to
the affine open sets containing in the image. -/
@[simps]
def _root_.AlgebraicGeometry.IsOpenImmersion.affineOpensEquiv (f : X ‚ü∂ Y) [H : IsOpenImmersion f] :
    X.affineOpens ‚âÉ { U : Y.affineOpens // U ‚â§ f.opensRange } where
  toFun U := ‚ü®‚ü®f ''·µÅ U, U.2.image_of_isOpenImmersion f‚ü©, Set.image_subset_range _ _‚ü©
  invFun U := ‚ü®f ‚Åª¬π·µÅ U, f.isAffineOpen_iff_of_isOpenImmersion.mp (by
    rw [show f ''·µÅ f ‚Åª¬π·µÅ U = U from Opens.ext (Set.image_preimage_eq_of_subset U.2)]; exact U.1.2)‚ü©
  left_inv _ := Subtype.ext (Opens.ext (Set.preimage_image_eq _ H.base_open.inj))
  right_inv U := Subtype.ext (Subtype.ext (Opens.ext (Set.image_preimage_eq_of_subset U.2)))

/-- The affine open sets of an open subscheme
corresponds to the affine open sets containing in the subset. -/
@[simps! apply_coe_coe]
def _root_.AlgebraicGeometry.affineOpensRestrict {X : Scheme.{u}} (U : X.Opens) :
    U.toScheme.affineOpens ‚âÉ { V : X.affineOpens // V ‚â§ U } :=
  (IsOpenImmersion.affineOpensEquiv U.Œπ).trans (Equiv.subtypeEquivProp (by simp))

@[simp]
lemma _root_.AlgebraicGeometry.affineOpensRestrict_symm_apply_coe
    {X : Scheme.{u}} (U : X.Opens) (V) :
    ((affineOpensRestrict U).symm V).1 = U.Œπ ‚Åª¬π·µÅ V := rfl

instance (priority := 100) _root_.AlgebraicGeometry.Scheme.compactSpace_of_isAffine
    (X : Scheme) [IsAffine X] :
    CompactSpace X :=
  ‚ü®(isAffineOpen_top X).isCompact‚ü©

@[simp]
theorem fromSpec_preimage_self :
    hU.fromSpec ‚Åª¬π·µÅ U = ‚ä§ := by
  ext1
  rw [Opens.map_coe, Opens.coe_top, ‚Üê hU.range_fromSpec, ‚Üê Set.image_univ]
  exact Set.preimage_image_eq _ PresheafedSpace.IsOpenImmersion.base_open.inj

theorem SpecŒìIdentity_hom_app_fromSpec :
    (Scheme.ŒìSpecIso Œì(X, U)).hom ‚â´ hU.fromSpec.app U =
      (Spec Œì(X, U)).presheaf.map (eqToHom hU.fromSpec_preimage_self).op := by
  simp only [fromSpec, Scheme.isoSpec, asIso_inv, Scheme.comp_coeBase, Opens.map_comp_obj,
    ŒìSpecIso_obj_hom, Scheme.Opens.topIso_inv, Opens.map_top, Functor.id_obj, Functor.comp_obj,
    Functor.rightOp_obj, Scheme.Œì_obj, unop_op, Scheme.Spec_obj, Scheme.Opens.topIso_hom,
    Scheme.comp_app, Scheme.Opens.Œπ_app_self, Category.assoc, ‚Üê Functor.map_comp_assoc, ‚Üê op_comp,
    eqToHom_trans, Scheme.Opens.eq_presheaf_map_eqToHom, Scheme.Hom.naturality_assoc,
    Scheme.inv_app_top, IsIso.hom_inv_id_assoc]
  simp only [eqToHom_op, eqToHom_map, Spec.map_eqToHom, eqToHom_unop,
    Scheme.Spec_map_presheaf_map_eqToHom, eqToHom_trans]

@[elementwise]
theorem fromSpec_app_self :
    hU.fromSpec.app U = (Scheme.ŒìSpecIso Œì(X, U)).inv ‚â´
      (Spec Œì(X, U)).presheaf.map (eqToHom hU.fromSpec_preimage_self).op := by
  rw [‚Üê hU.SpecŒìIdentity_hom_app_fromSpec, Iso.inv_hom_id_assoc]

theorem fromSpec_preimage_basicOpen' :
    hU.fromSpec ‚Åª¬π·µÅ X.basicOpen f = (Spec Œì(X, U)).basicOpen ((Scheme.ŒìSpecIso Œì(X, U)).inv f) := by
  rw [Scheme.preimage_basicOpen, hU.fromSpec_app_self]
  exact Scheme.basicOpen_res_eq _ _ (eqToHom hU.fromSpec_preimage_self).op

theorem fromSpec_preimage_basicOpen :
    hU.fromSpec ‚Åª¬π·µÅ X.basicOpen f = PrimeSpectrum.basicOpen f := by
  rw [fromSpec_preimage_basicOpen', ‚Üê basicOpen_eq_of_affine]

theorem fromSpec_image_basicOpen :
    hU.fromSpec ''·µÅ (PrimeSpectrum.basicOpen f) = X.basicOpen f := by
  rw [‚Üê hU.fromSpec_preimage_basicOpen]
  ext1
  change hU.fromSpec.val.base '' (hU.fromSpec.val.base ‚Åª¬π' (X.basicOpen f : Set X)) = _
  rw [Set.image_preimage_eq_inter_range, Set.inter_eq_left, hU.range_fromSpec]
  exact Scheme.basicOpen_le _ _

@[simp]
theorem basicOpen_fromSpec_app :
    (Spec Œì(X, U)).basicOpen (hU.fromSpec.app U f) = PrimeSpectrum.basicOpen f := by
  rw [‚Üê hU.fromSpec_preimage_basicOpen, Scheme.preimage_basicOpen]

include hU in
theorem basicOpen :
    IsAffineOpen (X.basicOpen f) := by
  rw [‚Üê hU.fromSpec_image_basicOpen, Scheme.Hom.isAffineOpen_iff_of_isOpenImmersion]
  convert isAffineOpen_opensRange
    (Spec.map (CommRingCat.ofHom <| algebraMap Œì(X, U) (Localization.Away f)))
  exact Opens.ext (PrimeSpectrum.localization_away_comap_range (Localization.Away f) f).symm

instance [IsAffine X] (r : Œì(X, ‚ä§)) : IsAffine (X.basicOpen r) :=
  (isAffineOpen_top X).basicOpen _

include hU in
theorem Œπ_basicOpen_preimage (r : Œì(X, ‚ä§)) :
    IsAffineOpen ((X.basicOpen r).Œπ ‚Åª¬π·µÅ U) := by
  apply (X.basicOpen r).Œπ.isAffineOpen_iff_of_isOpenImmersion.mp
  dsimp [Scheme.Hom.opensFunctor, LocallyRingedSpace.IsOpenImmersion.opensFunctor]
  rw [Opens.functor_obj_map_obj, Opens.openEmbedding_obj_top, inf_comm,
    ‚Üê Scheme.basicOpen_res _ _ (homOfLE le_top).op]
  exact hU.basicOpen _

include hU in
theorem exists_basicOpen_le {V : X.Opens} (x : V) (h : ‚Üëx ‚àà U) :
    ‚àÉ f : Œì(X, U), X.basicOpen f ‚â§ V ‚àß ‚Üëx ‚àà X.basicOpen f := by
  have : IsAffine _ := hU
  obtain ‚ü®_, ‚ü®_, ‚ü®r, rfl‚ü©, rfl‚ü©, h‚ÇÅ, h‚ÇÇ‚ü© :=
    (isBasis_basicOpen U).exists_subset_of_mem_open (x.2 : ‚ü®x, h‚ü© ‚àà _)
      ((Opens.map U.inclusion).obj V).isOpen
  have :
    U.Œπ ''·µÅ (U.toScheme.basicOpen r) =
      X.basicOpen (X.presheaf.map (eqToHom U.openEmbedding_obj_top.symm).op r) := by
    refine (Scheme.image_basicOpen U.Œπ r).trans ?_
    rw [Scheme.basicOpen_res_eq]
    simp only [Scheme.Opens.toScheme_presheaf_obj, Scheme.Opens.Œπ_appIso, Iso.refl_inv,
      CommRingCat.id_apply]
  use X.presheaf.map (eqToHom U.openEmbedding_obj_top.symm).op r
  rw [‚Üê this]
  exact ‚ü®Set.image_subset_iff.mpr h‚ÇÇ, ‚ü®_, h‚ü©, h‚ÇÅ, rfl‚ü©

/-- Given an affine open U and some `f : U`,
this is the canonical map `Œì(ùí™‚Çì, D(f)) ‚ü∂ Œì(Spec ùí™‚Çì(U), D(f))`
This is an isomorphism, as witnessed by an `IsIso` instance. -/
def basicOpenSectionsToAffine :
    Œì(X, X.basicOpen f) ‚ü∂ Œì(Spec Œì(X, U), PrimeSpectrum.basicOpen f) :=
  hU.fromSpec.1.c.app (op <| X.basicOpen f) ‚â´
    (Spec Œì(X, U)).presheaf.map (eqToHom <| (hU.fromSpec_preimage_basicOpen f).symm).op

instance basicOpenSectionsToAffine_isIso :
    IsIso (basicOpenSectionsToAffine hU f) := by
  delta basicOpenSectionsToAffine
  refine IsIso.comp_isIso' ?_ inferInstance
  apply PresheafedSpace.IsOpenImmersion.isIso_of_subset
  rw [hU.range_fromSpec]
  exact RingedSpace.basicOpen_le _ _

include hU in
theorem isLocalization_basicOpen :
    IsLocalization.Away f Œì(X, X.basicOpen f) := by
  apply
    (IsLocalization.isLocalization_iff_of_ringEquiv (Submonoid.powers f)
      (asIso <| basicOpenSectionsToAffine hU f).commRingCatIsoToRingEquiv).mpr
  convert StructureSheaf.IsLocalization.to_basicOpen _ f using 1
  -- Porting note: more hand holding is required here, the next 4 lines were not necessary
  delta StructureSheaf.openAlgebra
  congr 1
  rw [CommRingCat.ringHom_comp_eq_comp, Iso.commRingIsoToRingEquiv_toRingHom, asIso_hom]
  dsimp [CommRingCat.ofHom, RingHom.algebraMap_toAlgebra]
  change X.presheaf.map _ ‚â´ basicOpenSectionsToAffine hU f = _
  delta basicOpenSectionsToAffine
  rw [hU.fromSpec.naturality_assoc, hU.fromSpec_app_self]
  simp only [Category.assoc, ‚Üê Functor.map_comp, ‚Üê op_comp]
  apply StructureSheaf.toOpen_res

instance _root_.AlgebraicGeometry.isLocalization_away_of_isAffine
    [IsAffine X] (r : Œì(X, ‚ä§)) :
    IsLocalization.Away r Œì(X, X.basicOpen r) :=
  isLocalization_basicOpen (isAffineOpen_top X) r

lemma appLE_eq_away_map {X Y : Scheme.{u}} (f : X ‚ü∂ Y) {U : Y.Opens} (hU : IsAffineOpen U)
    {V : X.Opens} (hV : IsAffineOpen V) (e) (r : Œì(Y, U)) :
    letI := hU.isLocalization_basicOpen r
    letI := hV.isLocalization_basicOpen (f.appLE U V e r)
    f.appLE (Y.basicOpen r) (X.basicOpen (f.appLE U V e r)) (by simp [Scheme.Hom.appLE]) =
        IsLocalization.Away.map _ _ (f.appLE U V e) r := by
  letI := hU.isLocalization_basicOpen r
  letI := hV.isLocalization_basicOpen (f.appLE U V e r)
  apply IsLocalization.ringHom_ext (.powers r)
  rw [‚Üê CommRingCat.comp_eq_ring_hom_comp, IsLocalization.Away.map, IsLocalization.map_comp,
    RingHom.algebraMap_toAlgebra, RingHom.algebraMap_toAlgebra, ‚Üê CommRingCat.comp_eq_ring_hom_comp,
    Scheme.Hom.appLE_map, Scheme.Hom.map_appLE]

include hU in
theorem isLocalization_of_eq_basicOpen {V : X.Opens} (i : V ‚ü∂ U) (e : V = X.basicOpen f) :
    @IsLocalization.Away _ _ f Œì(X, V) _ (X.presheaf.map i.op).toAlgebra := by
  subst e; convert isLocalization_basicOpen hU f using 3

instance _root_.AlgebraicGeometry.Œì_restrict_isLocalization
    (X : Scheme.{u}) [IsAffine X] (r : Œì(X, ‚ä§)) :
    IsLocalization.Away r Œì(X.basicOpen r, ‚ä§) :=
  (isAffineOpen_top X).isLocalization_of_eq_basicOpen r _ (Opens.openEmbedding_obj_top _)

include hU in
theorem basicOpen_basicOpen_is_basicOpen (g : Œì(X, X.basicOpen f)) :
    ‚àÉ f' : Œì(X, U), X.basicOpen f' = X.basicOpen g := by
  have := isLocalization_basicOpen hU f
  obtain ‚ü®x, ‚ü®_, n, rfl‚ü©, rfl‚ü© := IsLocalization.surj'' (Submonoid.powers f) g
  use f * x
  rw [Algebra.smul_def, Scheme.basicOpen_mul, Scheme.basicOpen_mul, RingHom.algebraMap_toAlgebra]
  rw [Scheme.basicOpen_res]
  refine (inf_eq_left.mpr ?_).symm
  -- Porting note: a little help is needed here
  convert inf_le_left (Œ± := X.Opens) using 1
  apply Scheme.basicOpen_of_isUnit
  apply
    Submonoid.leftInv_le_isUnit _
      (IsLocalization.toInvSubmonoid (Submonoid.powers f) (Œì(X, X.basicOpen f))
        _).prop

include hU in
theorem _root_.AlgebraicGeometry.exists_basicOpen_le_affine_inter
    {V : X.Opens} (hV : IsAffineOpen V) (x : X) (hx : x ‚àà U ‚äì V) :
    ‚àÉ (f : Œì(X, U)) (g : Œì(X, V)), X.basicOpen f = X.basicOpen g ‚àß x ‚àà X.basicOpen f := by
  obtain ‚ü®f, hf‚ÇÅ, hf‚ÇÇ‚ü© := hU.exists_basicOpen_le ‚ü®x, hx.2‚ü© hx.1
  obtain ‚ü®g, hg‚ÇÅ, hg‚ÇÇ‚ü© := hV.exists_basicOpen_le ‚ü®x, hf‚ÇÇ‚ü© hx.2
  obtain ‚ü®f', hf'‚ü© :=
    basicOpen_basicOpen_is_basicOpen hU f (X.presheaf.map (homOfLE hf‚ÇÅ : _ ‚ü∂ V).op g)
  replace hf' := (hf'.trans (RingedSpace.basicOpen_res _ _ _)).trans (inf_eq_right.mpr hg‚ÇÅ)
  exact ‚ü®f', g, hf', hf'.symm ‚ñ∏ hg‚ÇÇ‚ü©

/-- The prime ideal of `ùí™‚Çì(U)` corresponding to a point `x : U`. -/
noncomputable def primeIdealOf (x : U) :
    PrimeSpectrum Œì(X, U) :=
  ((@Scheme.isoSpec U hU).hom ‚â´
    Spec.map (X.presheaf.map (eqToHom U.openEmbedding_obj_top).op)).1.base x

theorem fromSpec_primeIdealOf (x : U) :
    hU.fromSpec.val.base (hU.primeIdealOf x) = x.1 := by
  dsimp only [IsAffineOpen.fromSpec, Subtype.coe_mk, IsAffineOpen.primeIdealOf]
  -- Porting note: in the porting note of `Scheme.comp_val_base`, it says that `elementwise` is
  -- unnecessary, indeed, the linter did not like it, so I just use `elementwise_of%` instead of
  -- adding the corresponding lemma in `Scheme.lean` file
  erw [‚Üê elementwise_of% Scheme.comp_val_base] -- now `erw` after #13170
  simp only [Scheme.Opens.toScheme_presheaf_obj, Category.assoc, ‚Üê Spec.map_comp_assoc,
    ‚Üê Functor.map_comp, ‚Üê op_comp, eqToHom_trans, eqToHom_refl, op_id,
    CategoryTheory.Functor.map_id, Spec.map_id, Category.id_comp, Iso.hom_inv_id_assoc]
  rfl -- `rfl` was not needed before #13170

theorem isLocalization_stalk' (y : PrimeSpectrum Œì(X, U)) (hy : hU.fromSpec.1.base y ‚àà U) :
    @IsLocalization.AtPrime
      (R := Œì(X, U))
      (S := X.presheaf.stalk <| hU.fromSpec.1.base y) _ _
      ((TopCat.Presheaf.algebra_section_stalk X.presheaf _)) y.asIdeal _ := by
  apply
    (@IsLocalization.isLocalization_iff_of_ringEquiv (R := Œì(X, U))
      (S := X.presheaf.stalk (hU.fromSpec.1.base y)) _ y.asIdeal.primeCompl _
      (TopCat.Presheaf.algebra_section_stalk X.presheaf ‚ü®hU.fromSpec.1.base y, hy‚ü©) _ _
      (asIso <| hU.fromSpec.stalkMap y).commRingCatIsoToRingEquiv).mpr
  -- Porting note: need to know what the ring is and after convert, instead of equality
  -- we get an `iff`.
  convert StructureSheaf.IsLocalization.to_stalk Œì(X, U) y using 1
  delta IsLocalization.AtPrime StructureSheaf.stalkAlgebra
  rw [iff_iff_eq]
  congr 2
  rw [RingHom.algebraMap_toAlgebra]
  refine (PresheafedSpace.stalkMap_germ hU.fromSpec.1 _ ‚ü®_, hy‚ü©).trans ?_
  rw [‚Üê Scheme.Hom.app, IsAffineOpen.fromSpec_app_self, Category.assoc, TopCat.Presheaf.germ_res]
  rfl

-- Porting note: I have split this into two lemmas
theorem isLocalization_stalk (x : U) :
    IsLocalization.AtPrime (X.presheaf.stalk x) (hU.primeIdealOf x).asIdeal := by
  rcases x with ‚ü®x, hx‚ü©
  set y := hU.primeIdealOf ‚ü®x, hx‚ü© with hy
  have : hU.fromSpec.val.base y = x := hy ‚ñ∏ hU.fromSpec_primeIdealOf ‚ü®x, hx‚ü©
  clear_value y
  subst this
  exact hU.isLocalization_stalk' y hx

/-- The basic open set of a section `f` on an affine open as an `X.affineOpens`. -/
@[simps]
def _root_.AlgebraicGeometry.Scheme.affineBasicOpen
    (X : Scheme) {U : X.affineOpens} (f : Œì(X, U)) : X.affineOpens :=
  ‚ü®X.basicOpen f, U.prop.basicOpen f‚ü©

include hU in
/--
In an affine open set `U`, a family of basic open covers `U` iff the sections span `Œì(X, U)`.
See `iSup_basicOpen_of_span_eq_top` for the inverse direction without the affine-ness assumption.
-/
theorem basicOpen_union_eq_self_iff (s : Set Œì(X, U)) :
    ‚®Ü f : s, X.basicOpen (f : Œì(X, U)) = U ‚Üî Ideal.span s = ‚ä§ := by
  trans ‚ãÉ i : s, (PrimeSpectrum.basicOpen i.1).1 = Set.univ
  ¬∑ trans
      hU.fromSpec.1.base ‚Åª¬π' (‚®Ü f : s, X.basicOpen (f : Œì(X, U))).1 =
        hU.fromSpec.1.base ‚Åª¬π' U.1
    ¬∑ refine ‚ü®fun h => by rw [h], ?_‚ü©
      intro h
      apply_fun Set.image hU.fromSpec.1.base at h
      rw [Set.image_preimage_eq_inter_range, Set.image_preimage_eq_inter_range, hU.range_fromSpec]
        at h
      simp only [Set.inter_self, Opens.carrier_eq_coe, Set.inter_eq_right] at h
      ext1
      refine Set.Subset.antisymm ?_ h
      simp only [Set.iUnion_subset_iff, SetCoe.forall, Opens.coe_iSup]
      intro x _
      exact X.basicOpen_le x
    ¬∑ simp only [Opens.iSup_def, Subtype.coe_mk, Set.preimage_iUnion]
      congr! 1
      ¬∑ refine congr_arg (Set.iUnion ¬∑) ?_
        ext1 x
        exact congr_arg Opens.carrier (hU.fromSpec_preimage_basicOpen _)
      ¬∑ exact congr_arg Opens.carrier hU.fromSpec_preimage_self
  ¬∑ simp only [Opens.carrier_eq_coe, PrimeSpectrum.basicOpen_eq_zeroLocus_compl]
    rw [‚Üê Set.compl_iInter, Set.compl_univ_iff, ‚Üê PrimeSpectrum.zeroLocus_iUnion, ‚Üê
      PrimeSpectrum.zeroLocus_empty_iff_eq_top, PrimeSpectrum.zeroLocus_span]
    simp only [Set.iUnion_singleton_eq_range, Subtype.range_val_subtype, Set.setOf_mem_eq]

include hU in
theorem self_le_basicOpen_union_iff (s : Set Œì(X, U)) :
    (U ‚â§ ‚®Ü f : s, X.basicOpen f.1) ‚Üî Ideal.span s = ‚ä§ := by
  rw [‚Üê hU.basicOpen_union_eq_self_iff, @comm _ Eq]
  refine ‚ü®fun h => le_antisymm h ?_, le_of_eq‚ü©
  simp only [iSup_le_iff, SetCoe.forall]
  intro x _
  exact X.basicOpen_le x

end IsAffineOpen

/--
Given a spanning set of `Œì(X, U)`, the corresponding basic open sets cover `U`.
See `IsAffineOpen.basicOpen_union_eq_self_iff` for the inverse direction for affine open sets.
-/
lemma iSup_basicOpen_of_span_eq_top {X : Scheme} (U) (s : Set Œì(X, U))
    (hs : Ideal.span s = ‚ä§) : (‚®Ü i ‚àà s, X.basicOpen i) = U := by
  apply le_antisymm
  ¬∑ rw [iSup‚ÇÇ_le_iff]
    exact fun i _ ‚Ü¶ X.basicOpen_le i
  ¬∑ intro x hx
    obtain ‚ü®_, ‚ü®V, hV, rfl‚ü©, hxV, hVU‚ü© := (isBasis_affine_open X).exists_subset_of_mem_open hx U.2
    refine SetLike.mem_of_subset ?_ hxV
    rw [‚Üê (hV.basicOpen_union_eq_self_iff (X.presheaf.map (homOfLE hVU).op '' s)).mpr
      (by rw [‚Üê Ideal.map_span, hs, Ideal.map_top])]
    simp only [Opens.iSup_mk, Opens.carrier_eq_coe, Set.iUnion_coe_set, Set.mem_image,
      Set.iUnion_exists, Set.biUnion_and', Set.iUnion_iUnion_eq_right, Scheme.basicOpen_res,
      Opens.coe_inf, Opens.coe_mk, Set.iUnion_subset_iff]
    exact fun i hi ‚Ü¶ (Set.inter_subset_right.trans
      (Set.subset_iUnion‚ÇÇ (s := fun x _ ‚Ü¶ (X.basicOpen x : Set X)) i hi))

/-- Let `P` be a predicate on the affine open sets of `X` satisfying
1. If `P` holds on `U`, then `P` holds on the basic open set of every section on `U`.
2. If `P` holds for a family of basic open sets covering `U`, then `P` holds for `U`.
3. There exists an affine open cover of `X` each satisfying `P`.

Then `P` holds for every affine open of `X`.

This is also known as the **Affine communication lemma** in [*The rising sea*][RisingSea]. -/
@[elab_as_elim]
theorem of_affine_open_cover {X : Scheme} {P : X.affineOpens ‚Üí Prop}
    {Œπ} (U : Œπ ‚Üí X.affineOpens) (iSup_U : (‚®Ü i, U i : X.Opens) = ‚ä§)
    (V : X.affineOpens)
    (basicOpen : ‚àÄ (U : X.affineOpens) (f : Œì(X, U)), P U ‚Üí P (X.affineBasicOpen f))
    (openCover :
      ‚àÄ (U : X.affineOpens) (s : Finset (Œì(X, U)))
        (_ : Ideal.span (s : Set (Œì(X, U))) = ‚ä§),
        (‚àÄ f : s, P (X.affineBasicOpen f.1)) ‚Üí P U)
    (hU : ‚àÄ i, P (U i)) : P V := by
  classical
  have : ‚àÄ (x : V.1), ‚àÉ f : Œì(X, V), ‚Üëx ‚àà X.basicOpen f ‚àß P (X.affineBasicOpen f) := by
    intro x
    obtain ‚ü®i, hi‚ü© := Opens.mem_iSup.mp (show x.1 ‚àà (‚®Ü i, U i : X.Opens) from iSup_U ‚ñ∏ trivial)
    obtain ‚ü®f, g, e, hf‚ü© := exists_basicOpen_le_affine_inter V.prop (U i).prop x ‚ü®x.prop, hi‚ü©
    refine ‚ü®f, hf, ?_‚ü©
    convert basicOpen _ g (hU i) using 1
    ext1
    exact e
  choose f hf‚ÇÅ hf‚ÇÇ using this
  suffices Ideal.span (Set.range f) = ‚ä§ by
    obtain ‚ü®t, ht‚ÇÅ, ht‚ÇÇ‚ü© := (Ideal.span_eq_top_iff_finite _).mp this
    apply openCover V t ht‚ÇÇ
    rintro ‚ü®i, hi‚ü©
    obtain ‚ü®x, rfl‚ü© := ht‚ÇÅ hi
    exact hf‚ÇÇ x
  rw [‚Üê V.prop.self_le_basicOpen_union_iff]
  intro x hx
  rw [iSup_range', SetLike.mem_coe, Opens.mem_iSup]
  exact ‚ü®_, hf‚ÇÅ ‚ü®x, hx‚ü©‚ü©

section ZeroLocus

/-- On a locally ringed space `X`, the preimage of the zero locus of the prime spectrum
of `Œì(X, ‚ä§)` under `toŒìSpecFun` agrees with the associated zero locus on `X`. -/
lemma Scheme.toŒìSpec_preimage_zeroLocus_eq {X : Scheme.{u}} (s : Set Œì(X, ‚ä§)) :
    (ŒìSpec.adjunction.unit.app X).val.base ‚Åª¬π' PrimeSpectrum.zeroLocus s = X.zeroLocus s :=
  LocallyRingedSpace.toŒìSpec_preimage_zeroLocus_eq s

open ConcreteCategory

/-- If `X` is affine, the image of the zero locus of global sections of `X` under `toŒìSpecFun`
is the zero locus in terms of the prime spectrum of `Œì(X, ‚ä§)`. -/
lemma Scheme.toŒìSpec_image_zeroLocus_eq_of_isAffine {X : Scheme.{u}} [IsAffine X]
    (s : Set Œì(X, ‚ä§)) :
    X.isoSpec.hom.val.base '' X.zeroLocus s = PrimeSpectrum.zeroLocus s := by
  erw [‚Üê X.toŒìSpec_preimage_zeroLocus_eq, Set.image_preimage_eq]
  exact (bijective_of_isIso X.isoSpec.hom.val.base).surjective

/-- If `X` is an affine scheme, every closed set of `X` is the zero locus
of a set of global sections. -/
lemma Scheme.eq_zeroLocus_of_isClosed_of_isAffine (X : Scheme.{u}) [IsAffine X] (s : Set X) :
    IsClosed s ‚Üî ‚àÉ I : Ideal (Œì(X, ‚ä§)), s = X.zeroLocus (I : Set Œì(X, ‚ä§)) := by
  refine ‚ü®fun hs ‚Ü¶ ?_, ?_‚ü©
  ¬∑ let Z : Set (Spec <| Œì(X, ‚ä§)) := X.toŒìSpecFun '' s
    have hZ : IsClosed Z := (X.isoSpec.hom.homeomorph).isClosedMap _ hs
    obtain ‚ü®I, (hI : Z = _)‚ü© := (PrimeSpectrum.isClosed_iff_zeroLocus_ideal _).mp hZ
    use I
    simp only [‚Üê Scheme.toŒìSpec_preimage_zeroLocus_eq, ‚Üê hI, Z]
    erw [Set.preimage_image_eq _ (bijective_of_isIso X.isoSpec.hom.val.base).injective]
  ¬∑ rintro ‚ü®I, rfl‚ü©
    exact zeroLocus_isClosed X I.carrier

end ZeroLocus

section Stalks

/-- Variant of `AlgebraicGeometry.localRingHom_comp_stalkIso` for `Spec.map`. -/
@[elementwise]
lemma Scheme.localRingHom_comp_stalkIso {R S : CommRingCat.{u}} (f : R ‚ü∂ S) (p : PrimeSpectrum S) :
    (StructureSheaf.stalkIso R (PrimeSpectrum.comap f p)).hom ‚â´
      (CommRingCat.ofHom <| Localization.localRingHom
        (PrimeSpectrum.comap f p).asIdeal p.asIdeal f rfl) ‚â´
      (StructureSheaf.stalkIso S p).inv = (Spec.map f).stalkMap p :=
  AlgebraicGeometry.localRingHom_comp_stalkIso f p

/-- Given a morphism of rings `f : R ‚ü∂ S`, the stalk map of `Spec S ‚ü∂ Spec R` at
a prime of `S` is isomorphic to the localized ring homomorphism. -/
def Scheme.arrowStalkMapSpecIso {R S : CommRingCat.{u}} (f : R ‚ü∂ S) (p : PrimeSpectrum S) :
    Arrow.mk ((Spec.map f).stalkMap p) ‚âÖ Arrow.mk (CommRingCat.ofHom <| Localization.localRingHom
      (PrimeSpectrum.comap f p).asIdeal p.asIdeal f rfl) := Arrow.isoMk
  (StructureSheaf.stalkIso R (PrimeSpectrum.comap f p))
  (StructureSheaf.stalkIso S p) <| by
    rw [‚Üê Scheme.localRingHom_comp_stalkIso]
    simp

end Stalks

@[deprecated (since := "2024-06-21"), nolint defLemma]
alias isAffineAffineScheme := isAffine_affineScheme
@[deprecated (since := "2024-06-21"), nolint defLemma]
alias SpecIsAffine := isAffine_Spec
@[deprecated (since := "2024-06-21")]
alias isAffineOfIso := isAffine_of_isIso
@[deprecated (since := "2024-06-21")]
alias rangeIsAffineOpenOfOpenImmersion := isAffineOpen_opensRange
@[deprecated (since := "2024-06-21")]
alias topIsAffineOpen := isAffineOpen_top
@[deprecated (since := "2024-06-21"), nolint defLemma]
alias Scheme.affineCoverIsAffine := Scheme.isAffine_affineCover
@[deprecated (since := "2024-06-21"), nolint defLemma]
alias Scheme.affineBasisCoverIsAffine := Scheme.isAffine_affineBasisCover
@[deprecated (since := "2024-06-21")]
alias IsAffineOpen.fromSpec_range := IsAffineOpen.range_fromSpec
@[deprecated (since := "2024-06-21")]
alias IsAffineOpen.imageIsOpenImmersion := IsAffineOpen.image_of_isOpenImmersion
@[deprecated (since := "2024-06-21"), nolint defLemma]
alias Scheme.quasi_compact_of_affine := Scheme.compactSpace_of_isAffine
@[deprecated (since := "2024-06-21")]
alias IsAffineOpen.fromSpec_base_preimage := IsAffineOpen.fromSpec_preimage_self
@[deprecated (since := "2024-06-21")]
alias IsAffineOpen.fromSpec_map_basicOpen' := IsAffineOpen.fromSpec_preimage_basicOpen'
@[deprecated (since := "2024-06-21")]
alias IsAffineOpen.fromSpec_map_basicOpen := IsAffineOpen.fromSpec_preimage_basicOpen
@[deprecated (since := "2024-06-21")]
alias IsAffineOpen.opensFunctor_map_basicOpen := IsAffineOpen.fromSpec_image_basicOpen
@[deprecated (since := "2024-06-21")]
alias IsAffineOpen.basicOpenIsAffine := IsAffineOpen.basicOpen
@[deprecated (since := "2024-06-21")]
alias IsAffineOpen.mapRestrictBasicOpen := IsAffineOpen.Œπ_basicOpen_preimage

end AlgebraicGeometry
