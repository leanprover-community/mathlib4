/-
Copyright (c) 2026 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
module

public import Mathlib.AlgebraicGeometry.Morphisms.Affine
public import Mathlib.AlgebraicGeometry.Sites.SmallAffineZariski
public import Mathlib.CategoryTheory.Adjunction.Evaluation
public import Mathlib.CategoryTheory.Limits.Constructions.Over.Connected
public import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Over
public import Mathlib.CategoryTheory.MorphismProperty.OverAdjunction

/-!
# Schemes affine over a base

We show that the category of affine `X`-schemes is contravariantly equivalent to
`X.PreservesLocalizationCat`, a model of the category of quasi-coherent `ùí™‚Çì`-algebras.
We use this to conclude that the category of affine `X`-schemes is cocomplete, and that
the forgetful functor to `X`-schemes preserves (and reflects) them.

## Main definitions
- `AlgebraicGeometry.Scheme.PreservesLocalizationCat`:
  The category of presheaf `F` of commutative rings over the affine opens of `X` together
  with a structure morphism `Œ± : ùí™‚Çì ‚ü∂ F` satisfying `Œì(F, D(f)) = Œì(F, U)[1/Œ±(f)]`
  for each `f : Œì(ùí™‚Çì, U)`.
  This is essentially the category of quasi-coherent `ùí™‚Çì`-algebras.
- `AlgebraicGeometry.Scheme.preservesLocalizationCatEquivOver`:
  The contravariant equivalence between `X.PreservesLocalizationCat` and affine `X`-schemes.
-/

@[expose] public section

open CategoryTheory Limits

namespace AlgebraicGeometry.Scheme


universe u

variable {X Y : Scheme.{u}} (f : X ‚ü∂ Y)

open AffineZariskiSite

section PreservesLocalizationCat

/--
The category of presheaf `F` of commutative rings over the affine opens of `X` together
with a structure morphism `Œ± : ùí™‚Çì ‚ü∂ F` satisfying `Œì(F, D(f)) = Œì(F, U)[1/Œ±(f)]`
for each `f : Œì(ùí™‚Çì, U)`.

This is essentially the category of quasi-coherent `ùí™‚Çì`-algebras.
Also see `Scheme.preservesLocalizationCatEquivOver` for the (contravariant) equivalence to
affine `X`-schemes. -/
abbrev PreservesLocalizationCat (X : Scheme.{u}) : Type (u + 1) :=
  ObjectProperty.FullSubcategory
    fun F : Under ((toOpensFunctor X).op ‚ãô X.presheaf) ‚Ü¶ PreservesLocalization _ F.hom

/-- The forgetful functor on `preservesLocalizationCatForget`. -/
abbrev preservesLocalizationCatForget (X : Scheme) :
    X.PreservesLocalizationCat ‚•§ Under ((toOpensFunctor X).op ‚ãô X.presheaf) :=
  ObjectProperty.Œπ _

instance (F : X.PreservesLocalizationCat) :
    ((F.obj.right.rightOp ‚ãô Scheme.Spec) ‚ãô Scheme.forget).IsLocallyDirected :=
  F.2.isLocallyDirected

instance (F : X.PreservesLocalizationCat) {U V} (f : U ‚ü∂ V) :
    IsOpenImmersion ((F.obj.right.rightOp ‚ãô Scheme.Spec).map f) :=
  F.2.isOpenImmersion _ _ _

instance {J : Type*} [Category J] : ObjectProperty.IsClosedUnderColimitsOfShape
    (fun F : Under ((toOpensFunctor X).op ‚ãô X.presheaf) ‚Ü¶ PreservesLocalization _ F.hom) J := by
  wlog hJ : IsConnected J generalizing J
  ¬∑ refine ‚ü®fun F ‚ü®‚ü®c, Œ±, hc‚ü©, H‚ü© U f ‚Ü¶ ?_‚ü©
    have hc' := WithInitial.isColimitEquiv.symm hc
    have inst : IsConnected (WithInitial J) := isConnected_of_hasInitial _
    exact (this (J := WithInitial J) inst).1 F
      ‚ü®‚ü®WithInitial.liftToInitial c Under.mkIdInitial,
      { app i := i.casesOn Œ±.app (Under.mkIdInitial.to _) },
      isColimitOfReflects (Under.forget _) (by
      refine IsColimit.equivOfNatIsoOfIso ?_ _ _ ?_ (WithInitial.isColimitEquiv.symm hc)
      ¬∑ exact NatIso.ofComponents (fun i ‚Ü¶ i.casesOn (fun _ ‚Ü¶ .refl _) (.refl _)) <| by
          rintro (i | _) (j | _) <;> (try rintro ‚ü®‚ü©) <;> simp
      ¬∑ exact Cocones.ext (.refl _) (by rintro ‚ü®‚ü© <;> simp))‚ü©, fun i ‚Ü¶
        i.casesOn H X.preservesLocalization_toOpensFunctor‚ü© U f
  refine ‚ü®fun F ‚ü®‚ü®c, Œ±, hc‚ü©, H‚ü© U f ‚Ü¶ ?_‚ü©
  let hc‚ÇÅ := isColimitOfPreserves (Under.forget _ ‚ãô (CategoryTheory.evaluation _ _).obj (.op U)) hc
  let hc‚ÇÇ := isColimitOfPreserves (Under.forget _ ‚ãô (CategoryTheory.evaluation _ _).obj
    (.op (U.basicOpen f))) hc
  let (i : _) : Algebra ((c.obj i).right.obj (.op U)) ((c.obj i).right.obj
    (.op (U.basicOpen f))) := ((c.obj i).right.map (homOfLE (U.basicOpen_le f)).op).hom.toAlgebra
  let : Algebra (F.right.obj (.op U)) (F.right.obj (.op (U.basicOpen f))) :=
    (F.right.map (homOfLE (U.basicOpen_le f)).op).hom.toAlgebra
  have (i : _) : IsLocalization.Away (R := (c.obj i).right.obj (.op U))
    ((c.obj i).hom.app (.op U) f) ((c.obj i).right.obj (.op (U.basicOpen f))) := H i U f
  have (i : _) : IsLocalization.Away (R := F.right.obj (.op U))
      ((Œ±.app i).right.app (.op U) ((c.obj i).hom.app (.op U) f))
      (Localization.Away (M := F.right.obj (.op U)) (F.hom.app _ f)) := by
    convert (inferInstanceAs <| IsLocalization.Away (R := F.right.obj (.op U))
      (F.hom.app _ f) (Localization.Away (M := F.right.obj (.op U)) (F.hom.app _ f)))
    exact congr($(Under.w (Œ±.app i)).app (.op U) f)
  let e : F.right.obj (Opposite.op (U.basicOpen f)) ‚âÖ
      .of (Localization.Away (M := F.right.obj (.op U)) (F.hom.app _ f)) := by
    refine ‚ü®hc‚ÇÇ.desc ‚ü®_, fun i ‚Ü¶ CommRingCat.ofHom (IsLocalization.Away.map
        (R := (c.obj i).right.obj (.op U))
        (S := (c.obj i).right.obj (.op (U.basicOpen f))) (P := F.right.obj (.op U))
        (r := (c.obj i).hom.app _ f) _ ((Œ±.app i).right.app _).hom), ?_‚ü©,
      CommRingCat.ofHom (IsLocalization.Away.lift (R := F.right.obj (.op U)) (F.hom.app _ f)
        (g := (F.right.map (homOfLE (U.basicOpen_le f)).op).hom) ?_), ?_, ?_‚ü©
    ¬∑ intro i j g
      ext1
      dsimp
      refine IsLocalization.ringHom_ext (R := (c.obj i).right.obj (.op U))
        (.powers ((c.obj i).hom.app (.op U) f)) ?_
      simp only [IsLocalization.Away.map, RingHomCompTriple.comp_eq, IsLocalization.map_comp]
      rw [RingHom.comp_assoc, RingHom.algebraMap_toAlgebra, ‚Üê CommRingCat.hom_comp,
        NatTrans.naturality, CommRingCat.hom_comp, ‚Üê RingHom.comp_assoc,
        ‚Üê ((c.obj j).right.map _).hom.algebraMap_toAlgebra, IsLocalization.map_comp,
        RingHom.comp_assoc, ‚Üê CommRingCat.hom_comp, ‚Üê NatTrans.comp_app, ‚Üê Under.comp_right,
        NatTrans.naturality]
      simp
    ¬∑ have := U.2.isLocalization_basicOpen f
      convert_to IsUnit (F.hom.app (.op (U.basicOpen f))
        (X.presheaf.map ((toOpensFunctor X).map (homOfLE (U.basicOpen_le f))).op f))
      ¬∑ exact congr($(F.hom.naturality _) f).symm
      ¬∑ dsimp; exact .map _ (IsLocalization.Away.algebraMap_isUnit _)
    ¬∑ apply hc‚ÇÇ.hom_ext fun i ‚Ü¶ ?_
      rw [hc‚ÇÇ.fac_assoc]
      ext1
      dsimp
      refine IsLocalization.ringHom_ext (R := (c.obj i).right.obj (.op U))
        (.powers ((c.obj i).hom.app (.op U) f)) ?_
      dsimp
      simp only [IsLocalization.Away.map, RingHom.comp_assoc,
        IsLocalization.map_comp, RingHomCompTriple.comp_eq]
      rw [‚Üê RingHom.comp_assoc, IsLocalization.Away.lift_comp, RingHom.algebraMap_toAlgebra]
      simp only [‚Üê CommRingCat.hom_comp, (Œ±.app i).right.naturality]
      dsimp
    ¬∑ ext1
      refine IsLocalization.ringHom_ext (R := F.right.obj (.op U)) (.powers (F.hom.app _ f)) ?_
      dsimp
      simp only [homOfLE_leOfHom, RingHom.comp_assoc, IsLocalization.Away.lift_comp,
        RingHomCompTriple.comp_eq]
      rw [‚Üê CommRingCat.hom_comp]
      conv_rhs => rw [‚Üê CommRingCat.hom_ofHom (algebraMap _ _)]
      congr 1
      refine hc‚ÇÅ.hom_ext fun i ‚Ü¶ ?_
      dsimp
      erw [‚Üê (Œ±.app i).right.naturality_assoc, hc‚ÇÇ.fac]
      ext1
      dsimp [IsLocalization.Away.map]
      rw [‚Üê ((c.obj i).right.map _).hom.algebraMap_toAlgebra, IsLocalization.map_comp]
  dsimp
  refine (IsLocalization.isLocalization_iff_of_ringEquiv _ e.commRingCatIsoToRingEquiv).mpr ?_
  convert (inferInstanceAs (IsLocalization (R := F.right.obj (.op U))
    (.powers (F.hom.app _ f)) (Localization.Away (M := F.right.obj (.op U)) (F.hom.app _ f))):)
  refine Algebra.algebra_ext _ _ fun r ‚Ü¶ e.commRingCatIsoToRingEquiv.eq_symm_apply.mp ?_
  simp [e, Iso.commRingCatIsoToRingEquiv, RingHom.algebraMap_toAlgebra]

instance : HasColimits X.PreservesLocalizationCat where
  has_colimits_of_shape _ := hasColimitsOfShape_of_closedUnderColimits _ _

noncomputable instance : CreatesColimits X.preservesLocalizationCatForget :=
  ‚ü®fun {_} ‚Ü¶ createsColimitsOfShapeFullSubcategoryInclusion ..‚ü©

end PreservesLocalizationCat

section Equivalence

instance {f : MorphismProperty.Over @IsAffineHom ‚ä§ X} : IsAffineHom f.hom := f.prop

/-- (Implementation). The "relative Spec" functor from quasi-coherent `ùí™‚Çì`-algebras to
affine `X`-schemes. Use `preservesLocalizationCatEquivOver` directly. -/
noncomputable def preservesLocalizationCatToOver (X : Scheme.{u}) :
    X.PreservesLocalizationCat·µí·µñ ‚•§ MorphismProperty.Over @IsAffineHom ‚ä§ X where
  obj F := .mk _ (colimit.desc (F.unop.obj.right.rightOp ‚ãô Scheme.Spec)
    ‚ü®X, Functor.whiskerRight F.unop.obj.hom.rightOp _ ‚â´ (cocone X).Œπ‚ü©) <| by
    refine ‚ü®fun U hU ‚Ü¶ ?_‚ü©
    rw [show (colimit.desc (F.unop.obj.right.rightOp ‚ãô Scheme.Spec) ‚ü®X, _‚ü©) ‚Åª¬π·µÅ U = _ from
      F.unop.2.colimitDesc_preimage _ _ ‚ü®U, hU‚ü©]
    dsimp
    exact isAffineOpen_opensRange _
  map {F G} Œ± := MorphismProperty.Over.homMk
    (colimMap (Functor.whiskerRight (Œ±.unop.hom.right.rightOp) _)) <| by
    dsimp
    ext U
    simp only [Œπ_colimMap_assoc, colimit.Œπ_desc]
    dsimp
    rw [‚Üê Spec.map_comp_assoc, ‚Üê NatTrans.comp_app, Under.w Œ±.unop.hom]
  map_id := by intros; dsimp; ext; dsimp; ext; simp
  map_comp := by intros; dsimp; ext; dsimp; ext; simp

/-- (Implementation). The sections functor from affine `X`-schemes to quasi-coherent `ùí™‚Çì`-algebras.
Use `preservesLocalizationCatEquivOver` directly. -/
noncomputable def preservesLocalizationCatOfOver (X : Scheme.{u}) :
    (MorphismProperty.Over @IsAffineHom ‚ä§ X)·µí·µñ ‚•§ X.PreservesLocalizationCat where
  obj f := .mk (.mk (Y := ((preimage f.unop.hom).op ‚ãô (toOpensFunctor _).op) ‚ãô f.unop.left.presheaf)
      (Functor.whiskerLeft _ f.unop.hom.c)) fun U r ‚Ü¶
    (U.2.preimage f.unop.hom).isLocalization_of_eq_basicOpen _ _ (f.unop.hom.preimage_basicOpen r)
  map {f g} Œ± :=
    letI Œ≤ : preimage g.unop.hom ‚ãô toOpensFunctor g.unop.left ‚ü∂ preimage f.unop.hom ‚ãô
        toOpensFunctor f.unop.left ‚ãô TopologicalSpace.Opens.map Œ±.unop.hom.left.base :=
      { app U := eqToHom congr($(Over.w Œ±.unop.hom) ‚Åª¬π·µÅ U.1).symm }
    ‚ü®Under.homMk (Functor.whiskerLeft _ Œ±.unop.hom.left.c ‚â´
      Functor.whiskerRight (NatTrans.op Œ≤) g.unop.left.presheaf) (by
    ext U : 2
    exact ((Hom.congr_app (Over.w Œ±.unop.hom).symm U.unop.1).trans (Category.assoc _ _ _)).symm)‚ü©
  map_id f := by
    ext U : 4
    simpa [-CategoryTheory.Functor.map_id] using f.unop.left.presheaf.map_id _
  map_comp {f g h} Œ± Œ≤ := by
    ext U : 4
    simp [Scheme.Hom.app_eq_appLE, Scheme.Hom.appLE_comp_appLE, -Scheme.Hom.comp_appLE]

/-- The equivalence between quasi-coherent `ùí™‚Çì`-algebras and affine `X`-schemes.
The forwards direction is the relative Spec functor, taking `F` to `colim (F ‚ãô Spec)`.
The inverse direction takes `f : Y ‚ü∂ X` to `f_* ùí™_Y`. -/
noncomputable def preservesLocalizationCatEquivOver (X : Scheme.{u}) :
    X.PreservesLocalizationCat·µí·µñ ‚âå MorphismProperty.Over @IsAffineHom ‚ä§ X where
  functor := X.preservesLocalizationCatToOver
  inverse := X.preservesLocalizationCatOfOver.rightOp
  unitIso := by
    refine NatIso.ofComponents (fun F ‚Ü¶ .op (ObjectProperty.isoMk _ (Under.isoMk
    (NatIso.ofComponents (fun U ‚Ü¶ (colimit (F.unop.obj.right.rightOp ‚ãô Scheme.Spec)).presheaf.mapIso
        (eqToIso (colimit.Œπ (F.unop.obj.right.rightOp ‚ãô Scheme.Spec) U.unop
        |>.image_top_eq_opensRange.trans (F.unop.2.colimitDesc_preimage _ _ U.unop).symm)).op ‚â™‚â´
        (colimit.Œπ (_ ‚ãô Scheme.Spec) U.unop).appIso _ ‚â™‚â´ Scheme.ŒìSpecIso _) ?_) ?_))) ?_
    ¬∑ intros U V i
      dsimp [preservesLocalizationCatToOver, preservesLocalizationCatOfOver, toOpens]
      rw [‚Üê cancel_mono (ŒìSpecIso _).inv]
      simp only [Hom.appIso_hom', Hom.map_appLE_assoc, Category.assoc, ‚Üê Scheme.ŒìSpecIso_naturality,
        Iso.hom_inv_id, Category.comp_id, Scheme.Hom.app_eq_appLE, Scheme.Hom.appLE_comp_appLE]
      congr 1
      exact (colimit.w (F.unop.obj.right.rightOp ‚ãô Scheme.Spec) i.unop).symm
    ¬∑ ext U : 2
      dsimp [preservesLocalizationCatToOver, preservesLocalizationCatOfOver]
      simp only [Hom.appIso_hom', Hom.map_appLE_assoc, Scheme.Hom.app_eq_appLE,
        Scheme.Hom.appLE_comp_appLE_assoc, colimit.Œπ_desc]
      dsimp
      rw [Hom.comp_appLE, IsAffineOpen.fromSpec_app_of_le _ _ le_rfl]
      simp [Iso.inv_comp_eq, ‚Üê Scheme.ŒìSpecIso_naturality, Scheme.Hom.app_eq_appLE]
    ¬∑ intros F G Œ±
      apply Quiver.Hom.unop_inj
      dsimp
      ext U : 4
      dsimp [preservesLocalizationCatToOver, preservesLocalizationCatOfOver, toOpens]
      simp only [Hom.appIso_hom', Hom.map_appLE_assoc, Scheme.Hom.app_eq_appLE, Category.assoc,
        Scheme.Hom.appLE_comp_appLE_assoc, ‚Üê Scheme.ŒìSpecIso_naturality, Œπ_colimMap]
      simp
  counitIso := by
    refine NatIso.ofComponents (fun f ‚Ü¶ MorphismProperty.Over.isoMk (@asIso _ _ _ _
      (colimit.desc _ ‚ü®_, fun U ‚Ü¶ (U.2.preimage f.hom).fromSpec, ?_‚ü©) ?_) ?_) ?_
    ¬∑ intro U V i
      simpa [preservesLocalizationCatOfOver] using IsAffineOpen.map_fromSpec _ _ _
    ¬∑ refine (IsZariskiLocalAtTarget.iff_of_openCover (P := .isomorphisms _)
        (f.left.openCoverOfIsOpenCover _ (.comap (iSup_affineOpens_eq_top X)
        f.hom.base.hom))).mpr fun U ‚Ü¶ ?_
      convert (IsOpenImmersion.isoOfRangeEq (pullback.fst _ (f.hom ‚Åª¬π·µÅ U.1).Œπ)
        ((U.2.preimage f.hom).isoSpec.hom ‚â´
        colimit.Œπ ((X.preservesLocalizationCatOfOver.rightOp.obj f).unop.obj.right.rightOp ‚ãô
          Scheme.Spec) U) ?eq).isIso_hom
      case eq =>
        rw [‚Üê Hom.coe_opensRange, ‚Üê Hom.coe_opensRange]
        congr 1
        rw [Hom.opensRange_comp_of_isIso, Hom.opensRange_pullbackFst, Opens.opensRange_Œπ,
          ‚Üê (X.preservesLocalizationCatOfOver.rightOp.obj f).unop.2.colimitDesc_preimage
            (F := (X.preservesLocalizationCatOfOver.rightOp.obj f).unop.obj.right),
          ‚Üê Scheme.Hom.comp_preimage]
        congr 5
        apply colimit.hom_ext fun U ‚Ü¶ ?_
        have := IsAffineOpen.SpecMap_appLE_fromSpec f.hom U.2 (U.2.preimage f.hom) le_rfl
        simp [preservesLocalizationCatOfOver, ‚Üê this, Scheme.Hom.app_eq_appLE]
      ¬∑ rw [‚Üê cancel_mono (f.hom ‚Åª¬π·µÅ U.1).Œπ]
        refine (pullback.condition ..).symm.trans ((Iso.inv_comp_eq _).mp ?_)
        refine (IsOpenImmersion.isoOfRangeEq_inv_fac_assoc ..).trans ?_
        simp [IsAffineOpen.isoSpec_hom]
    ¬∑ dsimp [preservesLocalizationCatToOver]
      ext U
      have := IsAffineOpen.SpecMap_appLE_fromSpec f.hom U.2 (U.2.preimage f.hom) le_rfl
      simp [preservesLocalizationCatOfOver, ‚Üê this, Scheme.Hom.app_eq_appLE]
    ¬∑ intros g h Œ±
      ext
      dsimp [preservesLocalizationCatToOver]
      ext
      simpa [preservesLocalizationCatOfOver, ‚Üê Spec.map_comp_assoc, -Spec.map_comp,
        Scheme.Hom.app_eq_appLE] using IsAffineOpen.SpecMap_appLE_fromSpec _ _ _ _
  functor_unitIso_comp F := by
    ext
    dsimp [preservesLocalizationCatToOver]
    ext
    dsimp [preservesLocalizationCatOfOver]
    simp only [Œπ_colimMap_assoc, colimit.Œπ_desc]
    dsimp
    simp only [eqToHom_op, Hom.appIso_hom', Hom.map_appLE_assoc, Spec.map_comp,
      SpecMap_ŒìSpecIso_hom, Category.assoc, Category.comp_id]
    rw [IsAffineOpen.SpecMap_appLE_fromSpec (hV := isAffineOpen_top _), IsAffineOpen.fromSpec_top,
      isoSpec_Spec_inv, toSpecŒì_SpecMap_ŒìSpecIso_inv_assoc]

attribute [simps! functor_obj_left functor_obj_hom functor_map_hom_left counitIso_hom_app_hom_left]
  preservesLocalizationCatEquivOver

@[simp]
lemma preservesLocalizationCatEquivOver_inverse_obj_unop_obj_right_obj
    (Y : MorphismProperty.Over @IsAffineHom ‚ä§ X) (U : X.AffineZariskiSite·µí·µñ) :
    (X.preservesLocalizationCatEquivOver.inverse.obj Y).unop.obj.right.obj U =
      Œì(Y.left, Y.hom ‚Åª¬π·µÅ U.unop.toOpens) := rfl

@[simp]
lemma preservesLocalizationCatEquivOver_inverse_obj_unop_obj_right_map
    (Y : MorphismProperty.Over @IsAffineHom ‚ä§ X) {U V : X.AffineZariskiSite·µí·µñ} (i : U ‚ü∂ V) :
    (X.preservesLocalizationCatEquivOver.inverse.obj Y).unop.obj.right.map i =
      Y.left.presheaf.map (homOfLE (Y.hom.preimage_mono (toOpens_mono i.unop.le))).op := rfl

@[simp]
lemma preservesLocalizationCatEquivOver_inverse_obj_unop_obj_hom_app
    (Y : MorphismProperty.Over @IsAffineHom ‚ä§ X) (U : X.AffineZariskiSite·µí·µñ) :
    (X.preservesLocalizationCatEquivOver.inverse.obj Y).unop.obj.hom.app U =
      Y.hom.app U.unop.toOpens := rfl

@[simp]
lemma preservesLocalizationCatEquivOver_inverse_map_unop_hom_right_app
    {Y Z : MorphismProperty.Over @IsAffineHom ‚ä§ X} (f : Y ‚ü∂ Z) (U : X.AffineZariskiSite·µí·µñ) :
    (X.preservesLocalizationCatEquivOver.inverse.map f).unop.hom.right.app U =
    f.hom.left.appLE _ _ congr(($(MorphismProperty.Over.w f)) ‚Åª¬π·µÅ U.unop.1).ge := rfl

@[simp]
lemma preservesLocalizationCatEquivOver_unitIso_hom_app_unop_hom_right_app
    (F : X.PreservesLocalizationCat·µí·µñ) (U) :
    (X.preservesLocalizationCatEquivOver.unitIso.hom.app F).unop.hom.right.app U =
    (colimit.Œπ (F.unop.obj.right.rightOp ‚ãô Scheme.Spec) U.unop).appLE _ ‚ä§
      (by simp [‚Üê Scheme.Hom.comp_preimage]) ‚â´ (ŒìSpecIso _).hom := by
  dsimp [preservesLocalizationCatEquivOver]
  simp [Hom.appIso_hom']
  rfl

instance : HasLimits (MorphismProperty.Over @IsAffineHom ‚ä§ X) :=
  hasLimits_of_hasLimits_createsLimits X.preservesLocalizationCatEquivOver.inverse

end Equivalence

section PreservesLimits

/-- Under the equivalence of categories between affine `X`-schemes and quasi-coherent `ùí™‚Çì`-algebras,
the pullback functor on affine `X`-schemes along an open immersion `U ‚ü∂ X` corresponds
to the restriction functor `F ‚Ü¶ F|·µ§` of quasi-coherent `ùí™‚Çì`-algebras. -/
noncomputable def preservesLocalizationCatRestrict [IsOpenImmersion f] :
    Y.preservesLocalizationCatEquivOver.inverse ‚ãô Y.preservesLocalizationCatForget.op ‚ãô
      (Under.post ((Functor.whiskeringLeft _ _ _).obj (AffineZariskiSite.image f).op) ‚ãô
      Under.map ((toOpensFunctor X).op.whiskerLeft (IsOpenImmersion.presheafIso f).hom)).op ‚âÖ
    MorphismProperty.Over.pullback @IsAffineHom ‚ä§ f ‚ãô
      X.preservesLocalizationCatEquivOver.inverse ‚ãô X.preservesLocalizationCatForget.op := by
  refine NatIso.ofComponents (fun F ‚Ü¶ .op (Under.isoMk (NatIso.ofComponents (fun U ‚Ü¶
    ((pullback.fst F.hom f).appIso _).symm ‚â™‚â´ F.left.presheaf.mapIso
      (eqToIso (IsOpenImmersion.image_preimage_eq_preimage_image_of_isPullback
        (.flip <| .of_hasPullback _ _) _).symm).op) ?_) ?_)) ?_
  ¬∑ intros U V i; simp [‚Üê Functor.map_comp]
  ¬∑ ext U : 2
    dsimp
    rw [Iso.eq_inv_comp]
    simp only [Hom.appIso_hom', Scheme.Hom.app_eq_appLE, Scheme.Hom.appLE_comp_appLE_assoc,
      ‚Üê pullback.condition]
    simp [Hom.comp_appLE, Hom.app_eq_appLE]
  ¬∑ intros F G Œ±
    apply Quiver.Hom.unop_inj
    ext U : 3
    dsimp
    rw [‚Üê IsIso.comp_inv_eq]
    simp only [Category.assoc, Iso.inv_comp_eq, IsIso.inv_comp, ‚Üê Functor.map_inv, ‚Üê op_inv,
      inv_eqToHom]
    simp [Hom.appIso_hom', Scheme.Hom.appLE_comp_appLE, -Scheme.Hom.comp_appLE]

attribute [local instance] preservesLimitsOfSize_op in
-- This is true for arbitrary `f`. The instance is provided at the end of the file.
private instance [IsOpenImmersion f] :
    PreservesLimits (MorphismProperty.Over.pullback @IsAffineHom ‚ä§ f) := by
  suffices PreservesLimits (Y.preservesLocalizationCatEquivOver.functor ‚ãô
      MorphismProperty.Over.pullback @IsAffineHom ‚ä§ f) by
    exact preservesLimits_of_natIso (F := _ ‚ãô Y.preservesLocalizationCatEquivOver.functor ‚ãô
      MorphismProperty.Over.pullback @IsAffineHom ‚ä§ f)
      (Functor.isoWhiskerRight Y.preservesLocalizationCatEquivOver.counitIso
        (MorphismProperty.Over.pullback @IsAffineHom ‚ä§ f))
  suffices PreservesLimits ((Y.preservesLocalizationCatEquivOver.functor ‚ãô
      MorphismProperty.Over.pullback @IsAffineHom ‚ä§ f) ‚ãô
      X.preservesLocalizationCatEquivOver.inverse ‚ãô X.preservesLocalizationCatForget.op)
    from preservesLimits_of_reflects_of_preserves _
      (X.preservesLocalizationCatEquivOver.inverse ‚ãô X.preservesLocalizationCatForget.op)
  let FF : Under ((toOpensFunctor Y).op ‚ãô Y.presheaf) ‚•§
      Under ((toOpensFunctor X).op ‚ãô X.presheaf) :=
    Under.post ((Functor.whiskeringLeft _ _ _).obj (AffineZariskiSite.image f).op) ‚ãô Under.map
    ((toOpensFunctor X).op.whiskerLeft (IsOpenImmersion.presheafIso f).hom)
  exact preservesLimits_of_natIso ((preservesLocalizationCatRestrict f).isoInverseComp
      (G := Y.preservesLocalizationCatEquivOver.symm))

instance : PreservesLimits (MorphismProperty.Over.forget @IsAffineHom ‚ä§ X) := by
  clear Y f
  wlog hX : IsAffine X
  ¬∑ exact ‚ü®fun {J} _ ‚Ü¶ ‚ü®fun {F} ‚Ü¶ ‚ü®fun {c} hc ‚Ü¶ ‚ü®isLimitOverOfOpenCover _ X.affineCover fun i ‚Ü¶
      ((this inferInstance).1.1.1 (isLimitOfPreserves
      (MorphismProperty.Over.pullback _ ‚ä§ (X.affineCover.f i)) hc)).some‚ü©‚ü©‚ü©‚ü©
  refine ‚ü®fun {J} _ ‚Ü¶ ‚ü®fun {F} ‚Ü¶ ‚ü®fun {c} hc ‚Ü¶ ‚ü®.ofExistsUnique fun s ‚Ü¶ ?_‚ü©‚ü©‚ü©‚ü©
  let f‚ÇÇ (U : s.pt.left.affineOpens) :=
    hc.lift ‚ü®.mk _ (U.1.Œπ ‚â´ s.pt.hom) (isAffineHom_of_isAffine _),
      fun i ‚Ü¶ MorphismProperty.Over.homMk (U.1.Œπ ‚â´ (s.œÄ.app i).left) (by simp),
      fun i j e ‚Ü¶ by ext; simp [‚Üê s.w e]‚ü©
  let f : s.pt.left ‚ü∂ c.pt.1.left :=
    s.pt.left.directedAffineCover.glueMorphismsOfLocallyDirected (fun U ‚Ü¶ (f‚ÇÇ U).left)
      fun {i j} e ‚Ü¶
      have : (MorphismProperty.Over.homMk (s.pt.left.homOfLE e.le) (by simp)) ‚â´ f‚ÇÇ j = f‚ÇÇ i :=
        hc.hom_ext fun k ‚Ü¶ by dsimp [f‚ÇÇ]; ext; simp
      congr(($this).left)
  refine ‚ü®Over.homMk f ?_, fun j ‚Ü¶ ?_, ?_‚ü©
  ¬∑ apply s.pt.left.directedAffineCover.hom_ext _ _ fun j ‚Ü¶ ?_
    rw [OpenCover.map_glueMorphismsOfLocallyDirected_assoc]
    simp
  ¬∑ ext
    dsimp
    apply s.pt.left.directedAffineCover.hom_ext _ _ fun j ‚Ü¶ ?_
    rw [OpenCover.map_glueMorphismsOfLocallyDirected_assoc]
    simp [‚Üê MorphismProperty.Comma.comp_left, -MorphismProperty.Comma.comp_hom, f‚ÇÇ]
  ¬∑ intro f' hf'
    ext
    dsimp
    apply s.pt.left.directedAffineCover.hom_ext _ _ fun j ‚Ü¶ ?_
    rw [OpenCover.map_glueMorphismsOfLocallyDirected]
    rw [‚Üê hc.hom_ext (f := MorphismProperty.Over.homMk (j.1.Œπ ‚â´ f'.left) (by simp))
      (f' := f‚ÇÇ j) fun k ‚Ü¶ ?_]
    ¬∑ rfl
    dsimp
    simp only [IsLimit.fac, ‚Üê hf', f‚ÇÇ]
    ext
    simp

instance : ReflectsLimits (MorphismProperty.Over.forget @IsAffineHom ‚ä§ X) :=
  reflectsLimits_of_reflectsIsomorphisms

instance : PreservesLimits (MorphismProperty.Over.pullback @IsAffineHom ‚ä§ f) := by
  suffices PreservesLimits (MorphismProperty.Over.pullback @IsAffineHom ‚ä§ f ‚ãô
      MorphismProperty.Over.forget _ _ _) from
    preservesLimits_of_reflects_of_preserves _ (MorphismProperty.Over.forget _ _ _)
  change PreservesLimits (MorphismProperty.Over.forget _ _ _ ‚ãô Over.pullback f)
  infer_instance

end PreservesLimits

end AlgebraicGeometry.Scheme
