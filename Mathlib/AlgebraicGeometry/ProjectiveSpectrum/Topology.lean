/-
Copyright (c) 2020 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Johan Commelin
-/
import Mathlib.RingTheory.GradedAlgebra.HomogeneousIdeal
import Mathlib.Topology.Category.TopCat.Basic
import Mathlib.Topology.Sets.Opens
import Mathlib.Data.Set.Subsingleton

#align_import algebraic_geometry.projective_spectrum.topology from "leanprover-community/mathlib"@"d39590fc8728fbf6743249802486f8c91ffe07bc"

/-!
# Projective spectrum of a graded ring

The projective spectrum of a graded commutative ring is the subtype of all homogenous ideals that
are prime and do not contain the irrelevant ideal.
It is naturally endowed with a topology: the Zariski topology.

## Notation
- `R` is a commutative semiring;
- `A` is a commutative ring and an `R`-algebra;
- `ùíú : ‚Ñï ‚Üí Submodule R A` is the grading of `A`;

## Main definitions

* `ProjectiveSpectrum ùíú`: The projective spectrum of a graded ring `A`, or equivalently, the set of
  all homogeneous ideals of `A` that is both prime and relevant i.e. not containing irrelevant
  ideal. Henceforth, we call elements of projective spectrum *relevant homogeneous prime ideals*.
* `ProjectiveSpectrum.zeroLocus ùíú s`: The zero locus of a subset `s` of `A`
  is the subset of `ProjectiveSpectrum ùíú` consisting of all relevant homogeneous prime ideals that
  contain `s`.
* `ProjectiveSpectrum.vanishingIdeal t`: The vanishing ideal of a subset `t` of
  `ProjectiveSpectrum ùíú` is the intersection of points in `t` (viewed as relevant homogeneous prime
  ideals).
* `ProjectiveSpectrum.Top`: the topological space of `ProjectiveSpectrum ùíú` endowed with the
  Zariski topology.
-/


noncomputable section

open DirectSum Pointwise SetLike TopCat TopologicalSpace CategoryTheory Opposite

variable {R A : Type*}
variable [CommSemiring R] [CommRing A] [Algebra R A]
variable (ùíú : ‚Ñï ‚Üí Submodule R A) [GradedAlgebra ùíú]

-- porting note (#5171): removed @[nolint has_nonempty_instance]
/-- The projective spectrum of a graded commutative ring is the subtype of all homogenous ideals
that are prime and do not contain the irrelevant ideal. -/
@[ext]
structure ProjectiveSpectrum where
  asHomogeneousIdeal : HomogeneousIdeal ùíú
  isPrime : asHomogeneousIdeal.toIdeal.IsPrime
  not_irrelevant_le : ¬¨HomogeneousIdeal.irrelevant ùíú ‚â§ asHomogeneousIdeal
#align projective_spectrum ProjectiveSpectrum

attribute [instance] ProjectiveSpectrum.isPrime

namespace ProjectiveSpectrum

/-- The zero locus of a set `s` of elements of a commutative ring `A` is the set of all relevant
homogeneous prime ideals of the ring that contain the set `s`.

An element `f` of `A` can be thought of as a dependent function on the projective spectrum of `ùíú`.
At a point `x` (a homogeneous prime ideal) the function (i.e., element) `f` takes values in the
quotient ring `A` modulo the prime ideal `x`. In this manner, `zeroLocus s` is exactly the subset
of `ProjectiveSpectrum ùíú` where all "functions" in `s` vanish simultaneously. -/
def zeroLocus (s : Set A) : Set (ProjectiveSpectrum ùíú) :=
  { x | s ‚äÜ x.asHomogeneousIdeal }
#align projective_spectrum.zero_locus ProjectiveSpectrum.zeroLocus

@[simp]
theorem mem_zeroLocus (x : ProjectiveSpectrum ùíú) (s : Set A) :
    x ‚àà zeroLocus ùíú s ‚Üî s ‚äÜ x.asHomogeneousIdeal :=
  Iff.rfl
#align projective_spectrum.mem_zero_locus ProjectiveSpectrum.mem_zeroLocus

@[simp]
theorem zeroLocus_span (s : Set A) : zeroLocus ùíú (Ideal.span s) = zeroLocus ùíú s := by
  ext x
  exact (Submodule.gi _ _).gc s x.asHomogeneousIdeal.toIdeal
#align projective_spectrum.zero_locus_span ProjectiveSpectrum.zeroLocus_span

variable {ùíú}

/-- The vanishing ideal of a set `t` of points of the projective spectrum of a commutative ring `R`
is the intersection of all the relevant homogeneous prime ideals in the set `t`.

An element `f` of `A` can be thought of as a dependent function on the projective spectrum of `ùíú`.
At a point `x` (a homogeneous prime ideal) the function (i.e., element) `f` takes values in the
quotient ring `A` modulo the prime ideal `x`. In this manner, `vanishingIdeal t` is exactly the
ideal of `A` consisting of all "functions" that vanish on all of `t`. -/
def vanishingIdeal (t : Set (ProjectiveSpectrum ùíú)) : HomogeneousIdeal ùíú :=
  ‚®Ö (x : ProjectiveSpectrum ùíú) (_ : x ‚àà t), x.asHomogeneousIdeal
#align projective_spectrum.vanishing_ideal ProjectiveSpectrum.vanishingIdeal

theorem coe_vanishingIdeal (t : Set (ProjectiveSpectrum ùíú)) :
    (vanishingIdeal t : Set A) =
      { f | ‚àÄ x : ProjectiveSpectrum ùíú, x ‚àà t ‚Üí f ‚àà x.asHomogeneousIdeal } := by
  ext f
  rw [vanishingIdeal, SetLike.mem_coe, ‚Üê HomogeneousIdeal.mem_iff, HomogeneousIdeal.toIdeal_iInf,
    Submodule.mem_iInf]
  refine forall_congr' fun x => ?_
  rw [HomogeneousIdeal.toIdeal_iInf, Submodule.mem_iInf, HomogeneousIdeal.mem_iff]
#align projective_spectrum.coe_vanishing_ideal ProjectiveSpectrum.coe_vanishingIdeal

theorem mem_vanishingIdeal (t : Set (ProjectiveSpectrum ùíú)) (f : A) :
    f ‚àà vanishingIdeal t ‚Üî ‚àÄ x : ProjectiveSpectrum ùíú, x ‚àà t ‚Üí f ‚àà x.asHomogeneousIdeal := by
  rw [‚Üê SetLike.mem_coe, coe_vanishingIdeal, Set.mem_setOf_eq]
#align projective_spectrum.mem_vanishing_ideal ProjectiveSpectrum.mem_vanishingIdeal

@[simp]
theorem vanishingIdeal_singleton (x : ProjectiveSpectrum ùíú) :
    vanishingIdeal ({x} : Set (ProjectiveSpectrum ùíú)) = x.asHomogeneousIdeal := by
  simp [vanishingIdeal]
#align projective_spectrum.vanishing_ideal_singleton ProjectiveSpectrum.vanishingIdeal_singleton

theorem subset_zeroLocus_iff_le_vanishingIdeal (t : Set (ProjectiveSpectrum ùíú)) (I : Ideal A) :
    t ‚äÜ zeroLocus ùíú I ‚Üî I ‚â§ (vanishingIdeal t).toIdeal :=
  ‚ü®fun h _ k => (mem_vanishingIdeal _ _).mpr fun _ j => (mem_zeroLocus _ _ _).mpr (h j) k, fun h =>
    fun x j =>
    (mem_zeroLocus _ _ _).mpr (le_trans h fun _ h => ((mem_vanishingIdeal _ _).mp h) x j)‚ü©
#align projective_spectrum.subset_zero_locus_iff_le_vanishing_ideal ProjectiveSpectrum.subset_zeroLocus_iff_le_vanishingIdeal

variable (ùíú)

/-- `zeroLocus` and `vanishingIdeal` form a galois connection. -/
theorem gc_ideal :
    @GaloisConnection (Ideal A) (Set (ProjectiveSpectrum ùíú))·µí·µà _ _
      (fun I => zeroLocus ùíú I) fun t => (vanishingIdeal t).toIdeal :=
  fun I t => subset_zeroLocus_iff_le_vanishingIdeal t I
#align projective_spectrum.gc_ideal ProjectiveSpectrum.gc_ideal

/-- `zeroLocus` and `vanishingIdeal` form a galois connection. -/
theorem gc_set :
    @GaloisConnection (Set A) (Set (ProjectiveSpectrum ùíú))·µí·µà _ _
      (fun s => zeroLocus ùíú s) fun t => vanishingIdeal t := by
  have ideal_gc : GaloisConnection Ideal.span _ := (Submodule.gi A _).gc
  simpa [zeroLocus_span, Function.comp] using GaloisConnection.compose ideal_gc (gc_ideal ùíú)
#align projective_spectrum.gc_set ProjectiveSpectrum.gc_set

theorem gc_homogeneousIdeal :
    @GaloisConnection (HomogeneousIdeal ùíú) (Set (ProjectiveSpectrum ùíú))·µí·µà _ _
      (fun I => zeroLocus ùíú I) fun t => vanishingIdeal t :=
  fun I t => by
  simpa [show I.toIdeal ‚â§ (vanishingIdeal t).toIdeal ‚Üî I ‚â§ vanishingIdeal t from Iff.rfl] using
    subset_zeroLocus_iff_le_vanishingIdeal t I.toIdeal
#align projective_spectrum.gc_homogeneous_ideal ProjectiveSpectrum.gc_homogeneousIdeal

theorem subset_zeroLocus_iff_subset_vanishingIdeal (t : Set (ProjectiveSpectrum ùíú)) (s : Set A) :
    t ‚äÜ zeroLocus ùíú s ‚Üî s ‚äÜ vanishingIdeal t :=
  (gc_set _) s t
#align projective_spectrum.subset_zero_locus_iff_subset_vanishing_ideal ProjectiveSpectrum.subset_zeroLocus_iff_subset_vanishingIdeal

theorem subset_vanishingIdeal_zeroLocus (s : Set A) : s ‚äÜ vanishingIdeal (zeroLocus ùíú s) :=
  (gc_set _).le_u_l s
#align projective_spectrum.subset_vanishing_ideal_zero_locus ProjectiveSpectrum.subset_vanishingIdeal_zeroLocus

theorem ideal_le_vanishingIdeal_zeroLocus (I : Ideal A) :
    I ‚â§ (vanishingIdeal (zeroLocus ùíú I)).toIdeal :=
  (gc_ideal _).le_u_l I
#align projective_spectrum.ideal_le_vanishing_ideal_zero_locus ProjectiveSpectrum.ideal_le_vanishingIdeal_zeroLocus

theorem homogeneousIdeal_le_vanishingIdeal_zeroLocus (I : HomogeneousIdeal ùíú) :
    I ‚â§ vanishingIdeal (zeroLocus ùíú I) :=
  (gc_homogeneousIdeal _).le_u_l I
#align projective_spectrum.homogeneous_ideal_le_vanishing_ideal_zero_locus ProjectiveSpectrum.homogeneousIdeal_le_vanishingIdeal_zeroLocus

theorem subset_zeroLocus_vanishingIdeal (t : Set (ProjectiveSpectrum ùíú)) :
    t ‚äÜ zeroLocus ùíú (vanishingIdeal t) :=
  (gc_ideal _).l_u_le t
#align projective_spectrum.subset_zero_locus_vanishing_ideal ProjectiveSpectrum.subset_zeroLocus_vanishingIdeal

theorem zeroLocus_anti_mono {s t : Set A} (h : s ‚äÜ t) : zeroLocus ùíú t ‚äÜ zeroLocus ùíú s :=
  (gc_set _).monotone_l h
#align projective_spectrum.zero_locus_anti_mono ProjectiveSpectrum.zeroLocus_anti_mono

theorem zeroLocus_anti_mono_ideal {s t : Ideal A} (h : s ‚â§ t) :
    zeroLocus ùíú (t : Set A) ‚äÜ zeroLocus ùíú (s : Set A) :=
  (gc_ideal _).monotone_l h
#align projective_spectrum.zero_locus_anti_mono_ideal ProjectiveSpectrum.zeroLocus_anti_mono_ideal

theorem zeroLocus_anti_mono_homogeneousIdeal {s t : HomogeneousIdeal ùíú} (h : s ‚â§ t) :
    zeroLocus ùíú (t : Set A) ‚äÜ zeroLocus ùíú (s : Set A) :=
  (gc_homogeneousIdeal _).monotone_l h
#align projective_spectrum.zero_locus_anti_mono_homogeneous_ideal ProjectiveSpectrum.zeroLocus_anti_mono_homogeneousIdeal

theorem vanishingIdeal_anti_mono {s t : Set (ProjectiveSpectrum ùíú)} (h : s ‚äÜ t) :
    vanishingIdeal t ‚â§ vanishingIdeal s :=
  (gc_ideal _).monotone_u h
#align projective_spectrum.vanishing_ideal_anti_mono ProjectiveSpectrum.vanishingIdeal_anti_mono

theorem zeroLocus_bot : zeroLocus ùíú ((‚ä• : Ideal A) : Set A) = Set.univ :=
  (gc_ideal ùíú).l_bot
#align projective_spectrum.zero_locus_bot ProjectiveSpectrum.zeroLocus_bot

@[simp]
theorem zeroLocus_singleton_zero : zeroLocus ùíú ({0} : Set A) = Set.univ :=
  zeroLocus_bot _
#align projective_spectrum.zero_locus_singleton_zero ProjectiveSpectrum.zeroLocus_singleton_zero

@[simp]
theorem zeroLocus_empty : zeroLocus ùíú (‚àÖ : Set A) = Set.univ :=
  (gc_set ùíú).l_bot
#align projective_spectrum.zero_locus_empty ProjectiveSpectrum.zeroLocus_empty

@[simp]
theorem vanishingIdeal_univ : vanishingIdeal (‚àÖ : Set (ProjectiveSpectrum ùíú)) = ‚ä§ := by
  simpa using (gc_ideal _).u_top
#align projective_spectrum.vanishing_ideal_univ ProjectiveSpectrum.vanishingIdeal_univ

theorem zeroLocus_empty_of_one_mem {s : Set A} (h : (1 : A) ‚àà s) : zeroLocus ùíú s = ‚àÖ :=
  Set.eq_empty_iff_forall_not_mem.mpr fun x hx =>
    (inferInstance : x.asHomogeneousIdeal.toIdeal.IsPrime).ne_top <|
      x.asHomogeneousIdeal.toIdeal.eq_top_iff_one.mpr <| hx h
#align projective_spectrum.zero_locus_empty_of_one_mem ProjectiveSpectrum.zeroLocus_empty_of_one_mem

@[simp]
theorem zeroLocus_singleton_one : zeroLocus ùíú ({1} : Set A) = ‚àÖ :=
  zeroLocus_empty_of_one_mem ùíú (Set.mem_singleton (1 : A))
#align projective_spectrum.zero_locus_singleton_one ProjectiveSpectrum.zeroLocus_singleton_one

@[simp]
theorem zeroLocus_univ : zeroLocus ùíú (Set.univ : Set A) = ‚àÖ :=
  zeroLocus_empty_of_one_mem _ (Set.mem_univ 1)
#align projective_spectrum.zero_locus_univ ProjectiveSpectrum.zeroLocus_univ

theorem zeroLocus_sup_ideal (I J : Ideal A) :
    zeroLocus ùíú ((I ‚äî J : Ideal A) : Set A) = zeroLocus _ I ‚à© zeroLocus _ J :=
  (gc_ideal ùíú).l_sup
#align projective_spectrum.zero_locus_sup_ideal ProjectiveSpectrum.zeroLocus_sup_ideal

theorem zeroLocus_sup_homogeneousIdeal (I J : HomogeneousIdeal ùíú) :
    zeroLocus ùíú ((I ‚äî J : HomogeneousIdeal ùíú) : Set A) = zeroLocus _ I ‚à© zeroLocus _ J :=
  (gc_homogeneousIdeal ùíú).l_sup
#align projective_spectrum.zero_locus_sup_homogeneous_ideal ProjectiveSpectrum.zeroLocus_sup_homogeneousIdeal

theorem zeroLocus_union (s s' : Set A) : zeroLocus ùíú (s ‚à™ s') = zeroLocus _ s ‚à© zeroLocus _ s' :=
  (gc_set ùíú).l_sup
#align projective_spectrum.zero_locus_union ProjectiveSpectrum.zeroLocus_union

theorem vanishingIdeal_union (t t' : Set (ProjectiveSpectrum ùíú)) :
    vanishingIdeal (t ‚à™ t') = vanishingIdeal t ‚äì vanishingIdeal t' := by
  ext1; exact (gc_ideal ùíú).u_inf
#align projective_spectrum.vanishing_ideal_union ProjectiveSpectrum.vanishingIdeal_union

theorem zeroLocus_iSup_ideal {Œ≥ : Sort*} (I : Œ≥ ‚Üí Ideal A) :
    zeroLocus _ ((‚®Ü i, I i : Ideal A) : Set A) = ‚ãÇ i, zeroLocus ùíú (I i) :=
  (gc_ideal ùíú).l_iSup
#align projective_spectrum.zero_locus_supr_ideal ProjectiveSpectrum.zeroLocus_iSup_ideal

theorem zeroLocus_iSup_homogeneousIdeal {Œ≥ : Sort*} (I : Œ≥ ‚Üí HomogeneousIdeal ùíú) :
    zeroLocus _ ((‚®Ü i, I i : HomogeneousIdeal ùíú) : Set A) = ‚ãÇ i, zeroLocus ùíú (I i) :=
  (gc_homogeneousIdeal ùíú).l_iSup
#align projective_spectrum.zero_locus_supr_homogeneous_ideal ProjectiveSpectrum.zeroLocus_iSup_homogeneousIdeal

theorem zeroLocus_iUnion {Œ≥ : Sort*} (s : Œ≥ ‚Üí Set A) :
    zeroLocus ùíú (‚ãÉ i, s i) = ‚ãÇ i, zeroLocus ùíú (s i) :=
  (gc_set ùíú).l_iSup
#align projective_spectrum.zero_locus_Union ProjectiveSpectrum.zeroLocus_iUnion

theorem zeroLocus_bUnion (s : Set (Set A)) :
    zeroLocus ùíú (‚ãÉ s' ‚àà s, s' : Set A) = ‚ãÇ s' ‚àà s, zeroLocus ùíú s' := by
  simp only [zeroLocus_iUnion]
#align projective_spectrum.zero_locus_bUnion ProjectiveSpectrum.zeroLocus_bUnion

theorem vanishingIdeal_iUnion {Œ≥ : Sort*} (t : Œ≥ ‚Üí Set (ProjectiveSpectrum ùíú)) :
    vanishingIdeal (‚ãÉ i, t i) = ‚®Ö i, vanishingIdeal (t i) :=
  HomogeneousIdeal.toIdeal_injective <| by
    convert (gc_ideal ùíú).u_iInf; exact HomogeneousIdeal.toIdeal_iInf _
#align projective_spectrum.vanishing_ideal_Union ProjectiveSpectrum.vanishingIdeal_iUnion

theorem zeroLocus_inf (I J : Ideal A) :
    zeroLocus ùíú ((I ‚äì J : Ideal A) : Set A) = zeroLocus ùíú I ‚à™ zeroLocus ùíú J :=
  Set.ext fun x => x.isPrime.inf_le
#align projective_spectrum.zero_locus_inf ProjectiveSpectrum.zeroLocus_inf

theorem union_zeroLocus (s s' : Set A) :
    zeroLocus ùíú s ‚à™ zeroLocus ùíú s' = zeroLocus ùíú (Ideal.span s ‚äì Ideal.span s' : Ideal A) := by
  rw [zeroLocus_inf]
  simp
#align projective_spectrum.union_zero_locus ProjectiveSpectrum.union_zeroLocus

theorem zeroLocus_mul_ideal (I J : Ideal A) :
    zeroLocus ùíú ((I * J : Ideal A) : Set A) = zeroLocus ùíú I ‚à™ zeroLocus ùíú J :=
  Set.ext fun x => x.isPrime.mul_le
#align projective_spectrum.zero_locus_mul_ideal ProjectiveSpectrum.zeroLocus_mul_ideal

theorem zeroLocus_mul_homogeneousIdeal (I J : HomogeneousIdeal ùíú) :
    zeroLocus ùíú ((I * J : HomogeneousIdeal ùíú) : Set A) = zeroLocus ùíú I ‚à™ zeroLocus ùíú J :=
  Set.ext fun x => x.isPrime.mul_le
#align projective_spectrum.zero_locus_mul_homogeneous_ideal ProjectiveSpectrum.zeroLocus_mul_homogeneousIdeal

theorem zeroLocus_singleton_mul (f g : A) :
    zeroLocus ùíú ({f * g} : Set A) = zeroLocus ùíú {f} ‚à™ zeroLocus ùíú {g} :=
  Set.ext fun x => by simpa using x.isPrime.mul_mem_iff_mem_or_mem
#align projective_spectrum.zero_locus_singleton_mul ProjectiveSpectrum.zeroLocus_singleton_mul

@[simp]
theorem zeroLocus_singleton_pow (f : A) (n : ‚Ñï) (hn : 0 < n) :
    zeroLocus ùíú ({f ^ n} : Set A) = zeroLocus ùíú {f} :=
  Set.ext fun x => by simpa using x.isPrime.pow_mem_iff_mem n hn
#align projective_spectrum.zero_locus_singleton_pow ProjectiveSpectrum.zeroLocus_singleton_pow

theorem sup_vanishingIdeal_le (t t' : Set (ProjectiveSpectrum ùíú)) :
    vanishingIdeal t ‚äî vanishingIdeal t' ‚â§ vanishingIdeal (t ‚à© t') := by
  intro r
  rw [‚Üê HomogeneousIdeal.mem_iff, HomogeneousIdeal.toIdeal_sup, mem_vanishingIdeal,
    Submodule.mem_sup]
  rintro ‚ü®f, hf, g, hg, rfl‚ü© x ‚ü®hxt, hxt'‚ü©
  erw [mem_vanishingIdeal] at hf hg
  apply Submodule.add_mem <;> solve_by_elim
#align projective_spectrum.sup_vanishing_ideal_le ProjectiveSpectrum.sup_vanishingIdeal_le

theorem mem_compl_zeroLocus_iff_not_mem {f : A} {I : ProjectiveSpectrum ùíú} :
    I ‚àà (zeroLocus ùíú {f} : Set (ProjectiveSpectrum ùíú))·∂ú ‚Üî f ‚àâ I.asHomogeneousIdeal := by
  rw [Set.mem_compl_iff, mem_zeroLocus, Set.singleton_subset_iff]; rfl
#align projective_spectrum.mem_compl_zero_locus_iff_not_mem ProjectiveSpectrum.mem_compl_zeroLocus_iff_not_mem

/-- The Zariski topology on the prime spectrum of a commutative ring is defined via the closed sets
of the topology: they are exactly those sets that are the zero locus of a subset of the ring. -/
instance zariskiTopology : TopologicalSpace (ProjectiveSpectrum ùíú) :=
  TopologicalSpace.ofClosed (Set.range (ProjectiveSpectrum.zeroLocus ùíú)) ‚ü®Set.univ, by simp‚ü©
    (by
      intro Zs h
      rw [Set.sInter_eq_iInter]
      let f : Zs ‚Üí Set _ := fun i => Classical.choose (h i.2)
      have H : (Set.iInter fun i ‚Ü¶ zeroLocus ùíú (f i)) ‚àà Set.range (zeroLocus ùíú) :=
        ‚ü®_, zeroLocus_iUnion ùíú _‚ü©
      convert H using 2
      funext i
      exact (Classical.choose_spec (h i.2)).symm)
    (by
      rintro _ ‚ü®s, rfl‚ü© _ ‚ü®t, rfl‚ü©
      exact ‚ü®_, (union_zeroLocus ùíú s t).symm‚ü©)
#align projective_spectrum.zariski_topology ProjectiveSpectrum.zariskiTopology

/-- The underlying topology of `Proj` is the projective spectrum of graded ring `A`. -/
def top : TopCat :=
  TopCat.of (ProjectiveSpectrum ùíú)
set_option linter.uppercaseLean3 false in
#align projective_spectrum.Top ProjectiveSpectrum.top

theorem isOpen_iff (U : Set (ProjectiveSpectrum ùíú)) : IsOpen U ‚Üî ‚àÉ s, U·∂ú = zeroLocus ùíú s := by
  simp only [@eq_comm _ U·∂ú]; rfl
#align projective_spectrum.is_open_iff ProjectiveSpectrum.isOpen_iff

theorem isClosed_iff_zeroLocus (Z : Set (ProjectiveSpectrum ùíú)) :
    IsClosed Z ‚Üî ‚àÉ s, Z = zeroLocus ùíú s := by rw [‚Üê isOpen_compl_iff, isOpen_iff, compl_compl]
#align projective_spectrum.is_closed_iff_zero_locus ProjectiveSpectrum.isClosed_iff_zeroLocus

theorem isClosed_zeroLocus (s : Set A) : IsClosed (zeroLocus ùíú s) := by
  rw [isClosed_iff_zeroLocus]
  exact ‚ü®s, rfl‚ü©
#align projective_spectrum.is_closed_zero_locus ProjectiveSpectrum.isClosed_zeroLocus

theorem zeroLocus_vanishingIdeal_eq_closure (t : Set (ProjectiveSpectrum ùíú)) :
    zeroLocus ùíú (vanishingIdeal t : Set A) = closure t := by
  apply Set.Subset.antisymm
  ¬∑ rintro x hx t' ‚ü®ht', ht‚ü©
    obtain ‚ü®fs, rfl‚ü© : ‚àÉ s, t' = zeroLocus ùíú s := by rwa [isClosed_iff_zeroLocus] at ht'
    rw [subset_zeroLocus_iff_subset_vanishingIdeal] at ht
    exact Set.Subset.trans ht hx
  ¬∑ rw [(isClosed_zeroLocus _ _).closure_subset_iff]
    exact subset_zeroLocus_vanishingIdeal ùíú t
#align projective_spectrum.zero_locus_vanishing_ideal_eq_closure ProjectiveSpectrum.zeroLocus_vanishingIdeal_eq_closure

theorem vanishingIdeal_closure (t : Set (ProjectiveSpectrum ùíú)) :
    vanishingIdeal (closure t) = vanishingIdeal t := by
  have := (gc_ideal ùíú).u_l_u_eq_u t
  ext1
  erw [zeroLocus_vanishingIdeal_eq_closure ùíú t] at this
  exact this
#align projective_spectrum.vanishing_ideal_closure ProjectiveSpectrum.vanishingIdeal_closure

section BasicOpen

/-- `basicOpen r` is the open subset containing all prime ideals not containing `r`. -/
def basicOpen (r : A) : TopologicalSpace.Opens (ProjectiveSpectrum ùíú) where
  carrier := { x | r ‚àâ x.asHomogeneousIdeal }
  is_open' := ‚ü®{r}, Set.ext fun _ => Set.singleton_subset_iff.trans <| Classical.not_not.symm‚ü©
#align projective_spectrum.basic_open ProjectiveSpectrum.basicOpen

@[simp]
theorem mem_basicOpen (f : A) (x : ProjectiveSpectrum ùíú) :
    x ‚àà basicOpen ùíú f ‚Üî f ‚àâ x.asHomogeneousIdeal :=
  Iff.rfl
#align projective_spectrum.mem_basic_open ProjectiveSpectrum.mem_basicOpen

theorem mem_coe_basicOpen (f : A) (x : ProjectiveSpectrum ùíú) :
    x ‚àà (‚Üë(basicOpen ùíú f) : Set (ProjectiveSpectrum ùíú)) ‚Üî f ‚àâ x.asHomogeneousIdeal :=
  Iff.rfl
#align projective_spectrum.mem_coe_basic_open ProjectiveSpectrum.mem_coe_basicOpen

theorem isOpen_basicOpen {a : A} : IsOpen (basicOpen ùíú a : Set (ProjectiveSpectrum ùíú)) :=
  (basicOpen ùíú a).isOpen
#align projective_spectrum.is_open_basic_open ProjectiveSpectrum.isOpen_basicOpen

@[simp]
theorem basicOpen_eq_zeroLocus_compl (r : A) :
    (basicOpen ùíú r : Set (ProjectiveSpectrum ùíú)) = (zeroLocus ùíú {r})·∂ú :=
  Set.ext fun x => by simp only [Set.mem_compl_iff, mem_zeroLocus, Set.singleton_subset_iff]; rfl
#align projective_spectrum.basic_open_eq_zero_locus_compl ProjectiveSpectrum.basicOpen_eq_zeroLocus_compl

@[simp]
theorem basicOpen_one : basicOpen ùíú (1 : A) = ‚ä§ :=
  TopologicalSpace.Opens.ext <| by simp
#align projective_spectrum.basic_open_one ProjectiveSpectrum.basicOpen_one

@[simp]
theorem basicOpen_zero : basicOpen ùíú (0 : A) = ‚ä• :=
  TopologicalSpace.Opens.ext <| by simp
#align projective_spectrum.basic_open_zero ProjectiveSpectrum.basicOpen_zero

theorem basicOpen_mul (f g : A) : basicOpen ùíú (f * g) = basicOpen ùíú f ‚äì basicOpen ùíú g :=
  TopologicalSpace.Opens.ext <| by simp [zeroLocus_singleton_mul]
#align projective_spectrum.basic_open_mul ProjectiveSpectrum.basicOpen_mul

theorem basicOpen_mul_le_left (f g : A) : basicOpen ùíú (f * g) ‚â§ basicOpen ùíú f := by
  rw [basicOpen_mul ùíú f g]
  exact inf_le_left
#align projective_spectrum.basic_open_mul_le_left ProjectiveSpectrum.basicOpen_mul_le_left

theorem basicOpen_mul_le_right (f g : A) : basicOpen ùíú (f * g) ‚â§ basicOpen ùíú g := by
  rw [basicOpen_mul ùíú f g]
  exact inf_le_right
#align projective_spectrum.basic_open_mul_le_right ProjectiveSpectrum.basicOpen_mul_le_right

@[simp]
theorem basicOpen_pow (f : A) (n : ‚Ñï) (hn : 0 < n) : basicOpen ùíú (f ^ n) = basicOpen ùíú f :=
  TopologicalSpace.Opens.ext <| by simpa using zeroLocus_singleton_pow ùíú f n hn
#align projective_spectrum.basic_open_pow ProjectiveSpectrum.basicOpen_pow

theorem basicOpen_eq_union_of_projection (f : A) :
    basicOpen ùíú f = ‚®Ü i : ‚Ñï, basicOpen ùíú (GradedAlgebra.proj ùíú i f) :=
  TopologicalSpace.Opens.ext <|
    Set.ext fun z => by
      erw [mem_coe_basicOpen, TopologicalSpace.Opens.mem_sSup]
      constructor <;> intro hz
      ¬∑ rcases show ‚àÉ i, GradedAlgebra.proj ùíú i f ‚àâ z.asHomogeneousIdeal by
          contrapose! hz with H
          classical
          rw [‚Üê DirectSum.sum_support_decompose ùíú f]
          apply Ideal.sum_mem _ fun i _ => H i with ‚ü®i, hi‚ü©
        exact ‚ü®basicOpen ùíú (GradedAlgebra.proj ùíú i f), ‚ü®i, rfl‚ü©, by rwa [mem_basicOpen]‚ü©
      ¬∑ obtain ‚ü®_, ‚ü®i, rfl‚ü©, hz‚ü© := hz
        exact fun rid => hz (z.1.2 i rid)
#align projective_spectrum.basic_open_eq_union_of_projection ProjectiveSpectrum.basicOpen_eq_union_of_projection

theorem isTopologicalBasis_basic_opens :
    TopologicalSpace.IsTopologicalBasis
      (Set.range fun r : A => (basicOpen ùíú r : Set (ProjectiveSpectrum ùíú))) := by
  apply TopologicalSpace.isTopologicalBasis_of_isOpen_of_nhds
  ¬∑ rintro _ ‚ü®r, rfl‚ü©
    exact isOpen_basicOpen ùíú
  ¬∑ rintro p U hp ‚ü®s, hs‚ü©
    rw [‚Üê compl_compl U, Set.mem_compl_iff, ‚Üê hs, mem_zeroLocus, Set.not_subset] at hp
    obtain ‚ü®f, hfs, hfp‚ü© := hp
    refine ‚ü®basicOpen ùíú f, ‚ü®f, rfl‚ü©, hfp, ?_‚ü©
    rw [‚Üê Set.compl_subset_compl, ‚Üê hs, basicOpen_eq_zeroLocus_compl, compl_compl]
    exact zeroLocus_anti_mono ùíú (Set.singleton_subset_iff.mpr hfs)
#align projective_spectrum.is_topological_basis_basic_opens ProjectiveSpectrum.isTopologicalBasis_basic_opens

end BasicOpen

section Order

/-!
## The specialization order

We endow `ProjectiveSpectrum ùíú` with a partial order,
where `x ‚â§ y` if and only if `y ‚àà closure {x}`.
-/


instance : PartialOrder (ProjectiveSpectrum ùíú) :=
  PartialOrder.lift asHomogeneousIdeal fun ‚ü®_, _, _‚ü© ‚ü®_, _, _‚ü© => by simp only [mk.injEq, imp_self]

@[simp]
theorem as_ideal_le_as_ideal (x y : ProjectiveSpectrum ùíú) :
    x.asHomogeneousIdeal ‚â§ y.asHomogeneousIdeal ‚Üî x ‚â§ y :=
  Iff.rfl
#align projective_spectrum.as_ideal_le_as_ideal ProjectiveSpectrum.as_ideal_le_as_ideal

@[simp]
theorem as_ideal_lt_as_ideal (x y : ProjectiveSpectrum ùíú) :
    x.asHomogeneousIdeal < y.asHomogeneousIdeal ‚Üî x < y :=
  Iff.rfl
#align projective_spectrum.as_ideal_lt_as_ideal ProjectiveSpectrum.as_ideal_lt_as_ideal

theorem le_iff_mem_closure (x y : ProjectiveSpectrum ùíú) :
    x ‚â§ y ‚Üî y ‚àà closure ({x} : Set (ProjectiveSpectrum ùíú)) := by
  rw [‚Üê as_ideal_le_as_ideal, ‚Üê zeroLocus_vanishingIdeal_eq_closure, mem_zeroLocus,
    vanishingIdeal_singleton]
  simp only [as_ideal_le_as_ideal, coe_subset_coe]
#align projective_spectrum.le_iff_mem_closure ProjectiveSpectrum.le_iff_mem_closure

end Order

end ProjectiveSpectrum
