/-
Copyright (c) 2020 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Johan Commelin
-/
import Mathlib.RingTheory.GradedAlgebra.Homogeneous.Ideal
import Mathlib.Topology.Category.TopCat.Basic
import Mathlib.Topology.Sets.Opens
import Mathlib.Data.Set.Subsingleton

/-!
# Projective spectrum of a graded ring

The projective spectrum of a graded commutative ring is the subtype of all homogeneous ideals that
are prime and do not contain the irrelevant ideal.
It is naturally endowed with a topology: the Zariski topology.

## Notation
- `R` is a commutative semiring;
- `A` is a commutative ring and an `R`-algebra;
- `ğ’œ : â„• â†’ Submodule R A` is the grading of `A`;

## Main definitions

* `ProjectiveSpectrum ğ’œ`: The projective spectrum of a graded ring `A`, or equivalently, the set of
  all homogeneous ideals of `A` that is both prime and relevant i.e. not containing irrelevant
  ideal. Henceforth, we call elements of projective spectrum *relevant homogeneous prime ideals*.
* `ProjectiveSpectrum.zeroLocus ğ’œ s`: The zero locus of a subset `s` of `A`
  is the subset of `ProjectiveSpectrum ğ’œ` consisting of all relevant homogeneous prime ideals that
  contain `s`.
* `ProjectiveSpectrum.vanishingIdeal t`: The vanishing ideal of a subset `t` of
  `ProjectiveSpectrum ğ’œ` is the intersection of points in `t` (viewed as relevant homogeneous prime
  ideals).
* `ProjectiveSpectrum.Top`: the topological space of `ProjectiveSpectrum ğ’œ` endowed with the
  Zariski topology.
-/


noncomputable section

open DirectSum Pointwise SetLike TopCat TopologicalSpace CategoryTheory Opposite

variable {R A : Type*}
variable [CommSemiring R] [CommRing A] [Algebra R A]
variable (ğ’œ : â„• â†’ Submodule R A) [GradedAlgebra ğ’œ]

/-- The projective spectrum of a graded commutative ring is the subtype of all homogeneous ideals
that are prime and do not contain the irrelevant ideal. -/
@[ext]
structure ProjectiveSpectrum where
  asHomogeneousIdeal : HomogeneousIdeal ğ’œ
  isPrime : asHomogeneousIdeal.toIdeal.IsPrime
  not_irrelevant_le : Â¬HomogeneousIdeal.irrelevant ğ’œ â‰¤ asHomogeneousIdeal

attribute [instance] ProjectiveSpectrum.isPrime

namespace ProjectiveSpectrum

instance (x : ProjectiveSpectrum ğ’œ) : Ideal.IsPrime x.asHomogeneousIdeal.toIdeal := x.isPrime

/-- The zero locus of a set `s` of elements of a commutative ring `A` is the set of all relevant
homogeneous prime ideals of the ring that contain the set `s`.

An element `f` of `A` can be thought of as a dependent function on the projective spectrum of `ğ’œ`.
At a point `x` (a homogeneous prime ideal) the function (i.e., element) `f` takes values in the
quotient ring `A` modulo the prime ideal `x`. In this manner, `zeroLocus s` is exactly the subset
of `ProjectiveSpectrum ğ’œ` where all "functions" in `s` vanish simultaneously. -/
def zeroLocus (s : Set A) : Set (ProjectiveSpectrum ğ’œ) :=
  { x | s âŠ† x.asHomogeneousIdeal }

@[simp]
theorem mem_zeroLocus (x : ProjectiveSpectrum ğ’œ) (s : Set A) :
    x âˆˆ zeroLocus ğ’œ s â†” s âŠ† x.asHomogeneousIdeal :=
  Iff.rfl

@[simp]
theorem zeroLocus_span (s : Set A) : zeroLocus ğ’œ (Ideal.span s) = zeroLocus ğ’œ s := by
  ext x
  exact (Submodule.gi _ _).gc s x.asHomogeneousIdeal.toIdeal

variable {ğ’œ}

/-- The vanishing ideal of a set `t` of points of the projective spectrum of a commutative ring `R`
is the intersection of all the relevant homogeneous prime ideals in the set `t`.

An element `f` of `A` can be thought of as a dependent function on the projective spectrum of `ğ’œ`.
At a point `x` (a homogeneous prime ideal) the function (i.e., element) `f` takes values in the
quotient ring `A` modulo the prime ideal `x`. In this manner, `vanishingIdeal t` is exactly the
ideal of `A` consisting of all "functions" that vanish on all of `t`. -/
def vanishingIdeal (t : Set (ProjectiveSpectrum ğ’œ)) : HomogeneousIdeal ğ’œ :=
  â¨… (x : ProjectiveSpectrum ğ’œ) (_ : x âˆˆ t), x.asHomogeneousIdeal

theorem coe_vanishingIdeal (t : Set (ProjectiveSpectrum ğ’œ)) :
    (vanishingIdeal t : Set A) =
      { f | âˆ€ x : ProjectiveSpectrum ğ’œ, x âˆˆ t â†’ f âˆˆ x.asHomogeneousIdeal } := by
  ext f
  rw [vanishingIdeal, SetLike.mem_coe, â† HomogeneousIdeal.mem_iff, HomogeneousIdeal.toIdeal_iInf,
    Submodule.mem_iInf]
  refine forall_congr' fun x => ?_
  rw [HomogeneousIdeal.toIdeal_iInf, Submodule.mem_iInf, HomogeneousIdeal.mem_iff]

theorem mem_vanishingIdeal (t : Set (ProjectiveSpectrum ğ’œ)) (f : A) :
    f âˆˆ vanishingIdeal t â†” âˆ€ x : ProjectiveSpectrum ğ’œ, x âˆˆ t â†’ f âˆˆ x.asHomogeneousIdeal := by
  rw [â† SetLike.mem_coe, coe_vanishingIdeal, Set.mem_setOf_eq]

@[simp]
theorem vanishingIdeal_singleton (x : ProjectiveSpectrum ğ’œ) :
    vanishingIdeal ({x} : Set (ProjectiveSpectrum ğ’œ)) = x.asHomogeneousIdeal := by
  simp [vanishingIdeal]

theorem subset_zeroLocus_iff_le_vanishingIdeal (t : Set (ProjectiveSpectrum ğ’œ)) (I : Ideal A) :
    t âŠ† zeroLocus ğ’œ I â†” I â‰¤ (vanishingIdeal t).toIdeal :=
  âŸ¨fun h _ k => (mem_vanishingIdeal _ _).mpr fun _ j => (mem_zeroLocus _ _ _).mpr (h j) k, fun h =>
    fun x j =>
    (mem_zeroLocus _ _ _).mpr (le_trans h fun _ h => ((mem_vanishingIdeal _ _).mp h) x j)âŸ©

variable (ğ’œ)

/-- `zeroLocus` and `vanishingIdeal` form a Galois connection. -/
theorem gc_ideal :
    @GaloisConnection (Ideal A) (Set (ProjectiveSpectrum ğ’œ))áµ’áµˆ _ _
      (fun I => zeroLocus ğ’œ I) fun t => (vanishingIdeal t).toIdeal :=
  fun I t => subset_zeroLocus_iff_le_vanishingIdeal t I

/-- `zeroLocus` and `vanishingIdeal` form a Galois connection. -/
theorem gc_set :
    @GaloisConnection (Set A) (Set (ProjectiveSpectrum ğ’œ))áµ’áµˆ _ _
      (fun s => zeroLocus ğ’œ s) fun t => vanishingIdeal t := by
  have ideal_gc : GaloisConnection Ideal.span _ := (Submodule.gi A _).gc
  simpa [zeroLocus_span, Function.comp_def] using GaloisConnection.compose ideal_gc (gc_ideal ğ’œ)

theorem gc_homogeneousIdeal :
    @GaloisConnection (HomogeneousIdeal ğ’œ) (Set (ProjectiveSpectrum ğ’œ))áµ’áµˆ _ _
      (fun I => zeroLocus ğ’œ I) fun t => vanishingIdeal t :=
  fun I t => by
  simpa [show I.toIdeal â‰¤ (vanishingIdeal t).toIdeal â†” I â‰¤ vanishingIdeal t from Iff.rfl] using
    subset_zeroLocus_iff_le_vanishingIdeal t I.toIdeal

theorem subset_zeroLocus_iff_subset_vanishingIdeal (t : Set (ProjectiveSpectrum ğ’œ)) (s : Set A) :
    t âŠ† zeroLocus ğ’œ s â†” s âŠ† vanishingIdeal t :=
  (gc_set _) s t

theorem subset_vanishingIdeal_zeroLocus (s : Set A) : s âŠ† vanishingIdeal (zeroLocus ğ’œ s) :=
  (gc_set _).le_u_l s

theorem ideal_le_vanishingIdeal_zeroLocus (I : Ideal A) :
    I â‰¤ (vanishingIdeal (zeroLocus ğ’œ I)).toIdeal :=
  (gc_ideal _).le_u_l I

theorem homogeneousIdeal_le_vanishingIdeal_zeroLocus (I : HomogeneousIdeal ğ’œ) :
    I â‰¤ vanishingIdeal (zeroLocus ğ’œ I) :=
  (gc_homogeneousIdeal _).le_u_l I

theorem subset_zeroLocus_vanishingIdeal (t : Set (ProjectiveSpectrum ğ’œ)) :
    t âŠ† zeroLocus ğ’œ (vanishingIdeal t) :=
  (gc_ideal _).l_u_le t

theorem zeroLocus_anti_mono {s t : Set A} (h : s âŠ† t) : zeroLocus ğ’œ t âŠ† zeroLocus ğ’œ s :=
  (gc_set _).monotone_l h

theorem zeroLocus_anti_mono_ideal {s t : Ideal A} (h : s â‰¤ t) :
    zeroLocus ğ’œ (t : Set A) âŠ† zeroLocus ğ’œ (s : Set A) :=
  (gc_ideal _).monotone_l h

theorem zeroLocus_anti_mono_homogeneousIdeal {s t : HomogeneousIdeal ğ’œ} (h : s â‰¤ t) :
    zeroLocus ğ’œ (t : Set A) âŠ† zeroLocus ğ’œ (s : Set A) :=
  (gc_homogeneousIdeal _).monotone_l h

theorem vanishingIdeal_anti_mono {s t : Set (ProjectiveSpectrum ğ’œ)} (h : s âŠ† t) :
    vanishingIdeal t â‰¤ vanishingIdeal s :=
  (gc_ideal _).monotone_u h

theorem zeroLocus_bot : zeroLocus ğ’œ ((âŠ¥ : Ideal A) : Set A) = Set.univ :=
  (gc_ideal ğ’œ).l_bot

@[simp]
theorem zeroLocus_singleton_zero : zeroLocus ğ’œ ({0} : Set A) = Set.univ :=
  zeroLocus_bot _

@[simp]
theorem zeroLocus_empty : zeroLocus ğ’œ (âˆ… : Set A) = Set.univ :=
  (gc_set ğ’œ).l_bot

@[simp]
theorem vanishingIdeal_univ : vanishingIdeal (âˆ… : Set (ProjectiveSpectrum ğ’œ)) = âŠ¤ := by
  simpa using (gc_ideal _).u_top

theorem zeroLocus_empty_of_one_mem {s : Set A} (h : (1 : A) âˆˆ s) : zeroLocus ğ’œ s = âˆ… :=
  Set.eq_empty_iff_forall_notMem.mpr fun x hx =>
    (inferInstance : x.asHomogeneousIdeal.toIdeal.IsPrime).ne_top <|
      x.asHomogeneousIdeal.toIdeal.eq_top_iff_one.mpr <| hx h

@[simp]
theorem zeroLocus_singleton_one : zeroLocus ğ’œ ({1} : Set A) = âˆ… :=
  zeroLocus_empty_of_one_mem ğ’œ (Set.mem_singleton (1 : A))

@[simp]
theorem zeroLocus_univ : zeroLocus ğ’œ (Set.univ : Set A) = âˆ… :=
  zeroLocus_empty_of_one_mem _ (Set.mem_univ 1)

theorem zeroLocus_sup_ideal (I J : Ideal A) :
    zeroLocus ğ’œ ((I âŠ” J : Ideal A) : Set A) = zeroLocus _ I âˆ© zeroLocus _ J :=
  (gc_ideal ğ’œ).l_sup

theorem zeroLocus_sup_homogeneousIdeal (I J : HomogeneousIdeal ğ’œ) :
    zeroLocus ğ’œ ((I âŠ” J : HomogeneousIdeal ğ’œ) : Set A) = zeroLocus _ I âˆ© zeroLocus _ J :=
  (gc_homogeneousIdeal ğ’œ).l_sup

theorem zeroLocus_union (s s' : Set A) : zeroLocus ğ’œ (s âˆª s') = zeroLocus _ s âˆ© zeroLocus _ s' :=
  (gc_set ğ’œ).l_sup

theorem vanishingIdeal_union (t t' : Set (ProjectiveSpectrum ğ’œ)) :
    vanishingIdeal (t âˆª t') = vanishingIdeal t âŠ“ vanishingIdeal t' := by
  ext1; exact (gc_ideal ğ’œ).u_inf

theorem zeroLocus_iSup_ideal {Î³ : Sort*} (I : Î³ â†’ Ideal A) :
    zeroLocus _ ((â¨† i, I i : Ideal A) : Set A) = â‹‚ i, zeroLocus ğ’œ (I i) :=
  (gc_ideal ğ’œ).l_iSup

theorem zeroLocus_iSup_homogeneousIdeal {Î³ : Sort*} (I : Î³ â†’ HomogeneousIdeal ğ’œ) :
    zeroLocus _ ((â¨† i, I i : HomogeneousIdeal ğ’œ) : Set A) = â‹‚ i, zeroLocus ğ’œ (I i) :=
  (gc_homogeneousIdeal ğ’œ).l_iSup

theorem zeroLocus_iUnion {Î³ : Sort*} (s : Î³ â†’ Set A) :
    zeroLocus ğ’œ (â‹ƒ i, s i) = â‹‚ i, zeroLocus ğ’œ (s i) :=
  (gc_set ğ’œ).l_iSup

theorem zeroLocus_bUnion (s : Set (Set A)) :
    zeroLocus ğ’œ (â‹ƒ s' âˆˆ s, s' : Set A) = â‹‚ s' âˆˆ s, zeroLocus ğ’œ s' := by
  simp only [zeroLocus_iUnion]

theorem vanishingIdeal_iUnion {Î³ : Sort*} (t : Î³ â†’ Set (ProjectiveSpectrum ğ’œ)) :
    vanishingIdeal (â‹ƒ i, t i) = â¨… i, vanishingIdeal (t i) :=
  HomogeneousIdeal.toIdeal_injective <| by
    convert (gc_ideal ğ’œ).u_iInf; exact HomogeneousIdeal.toIdeal_iInf _

theorem zeroLocus_inf (I J : Ideal A) :
    zeroLocus ğ’œ ((I âŠ“ J : Ideal A) : Set A) = zeroLocus ğ’œ I âˆª zeroLocus ğ’œ J :=
  Set.ext fun x => x.isPrime.inf_le

theorem union_zeroLocus (s s' : Set A) :
    zeroLocus ğ’œ s âˆª zeroLocus ğ’œ s' = zeroLocus ğ’œ (Ideal.span s âŠ“ Ideal.span s' : Ideal A) := by
  rw [zeroLocus_inf]
  simp

theorem zeroLocus_mul_ideal (I J : Ideal A) :
    zeroLocus ğ’œ ((I * J : Ideal A) : Set A) = zeroLocus ğ’œ I âˆª zeroLocus ğ’œ J :=
  Set.ext fun x => x.isPrime.mul_le

theorem zeroLocus_mul_homogeneousIdeal (I J : HomogeneousIdeal ğ’œ) :
    zeroLocus ğ’œ ((I * J : HomogeneousIdeal ğ’œ) : Set A) = zeroLocus ğ’œ I âˆª zeroLocus ğ’œ J :=
  Set.ext fun x => x.isPrime.mul_le

theorem zeroLocus_singleton_mul (f g : A) :
    zeroLocus ğ’œ ({f * g} : Set A) = zeroLocus ğ’œ {f} âˆª zeroLocus ğ’œ {g} :=
  Set.ext fun x => by simpa using x.isPrime.mul_mem_iff_mem_or_mem

@[simp]
theorem zeroLocus_singleton_pow (f : A) (n : â„•) (hn : 0 < n) :
    zeroLocus ğ’œ ({f ^ n} : Set A) = zeroLocus ğ’œ {f} :=
  Set.ext fun x => by simpa using x.isPrime.pow_mem_iff_mem n hn

theorem sup_vanishingIdeal_le (t t' : Set (ProjectiveSpectrum ğ’œ)) :
    vanishingIdeal t âŠ” vanishingIdeal t' â‰¤ vanishingIdeal (t âˆ© t') := by
  intro r
  rw [â† HomogeneousIdeal.mem_iff, HomogeneousIdeal.toIdeal_sup, mem_vanishingIdeal,
    Submodule.mem_sup]
  rintro âŸ¨f, hf, g, hg, rflâŸ© x âŸ¨hxt, hxt'âŸ©
  rw [HomogeneousIdeal.mem_iff, mem_vanishingIdeal] at hf hg
  apply Submodule.add_mem <;> solve_by_elim

theorem mem_compl_zeroLocus_iff_notMem {f : A} {I : ProjectiveSpectrum ğ’œ} :
    I âˆˆ (zeroLocus ğ’œ {f} : Set (ProjectiveSpectrum ğ’œ))á¶œ â†” f âˆ‰ I.asHomogeneousIdeal := by
  rw [Set.mem_compl_iff, mem_zeroLocus, Set.singleton_subset_iff]; rfl

@[deprecated (since := "2025-05-23")]
alias mem_compl_zeroLocus_iff_not_mem := mem_compl_zeroLocus_iff_notMem

/-- The Zariski topology on the prime spectrum of a commutative ring is defined via the closed sets
of the topology: they are exactly those sets that are the zero locus of a subset of the ring. -/
instance zariskiTopology : TopologicalSpace (ProjectiveSpectrum ğ’œ) :=
  TopologicalSpace.ofClosed (Set.range (ProjectiveSpectrum.zeroLocus ğ’œ)) âŸ¨Set.univ, by simpâŸ©
    (by
      intro Zs h
      rw [Set.sInter_eq_iInter]
      let f : Zs â†’ Set _ := fun i => Classical.choose (h i.2)
      have H : (Set.iInter fun i â†¦ zeroLocus ğ’œ (f i)) âˆˆ Set.range (zeroLocus ğ’œ) :=
        âŸ¨_, zeroLocus_iUnion ğ’œ _âŸ©
      convert H using 2
      funext i
      exact (Classical.choose_spec (h i.2)).symm)
    (by
      rintro _ âŸ¨s, rflâŸ© _ âŸ¨t, rflâŸ©
      exact âŸ¨_, (union_zeroLocus ğ’œ s t).symmâŸ©)

/-- The underlying topology of `Proj` is the projective spectrum of graded ring `A`. -/
def top : TopCat :=
  TopCat.of (ProjectiveSpectrum ğ’œ)

theorem isOpen_iff (U : Set (ProjectiveSpectrum ğ’œ)) : IsOpen U â†” âˆƒ s, Uá¶œ = zeroLocus ğ’œ s := by
  simp only [@eq_comm _ Uá¶œ]; rfl

theorem isClosed_iff_zeroLocus (Z : Set (ProjectiveSpectrum ğ’œ)) :
    IsClosed Z â†” âˆƒ s, Z = zeroLocus ğ’œ s := by rw [â† isOpen_compl_iff, isOpen_iff, compl_compl]

theorem isClosed_zeroLocus (s : Set A) : IsClosed (zeroLocus ğ’œ s) := by
  rw [isClosed_iff_zeroLocus]
  exact âŸ¨s, rflâŸ©

theorem zeroLocus_vanishingIdeal_eq_closure (t : Set (ProjectiveSpectrum ğ’œ)) :
    zeroLocus ğ’œ (vanishingIdeal t : Set A) = closure t := by
  apply Set.Subset.antisymm
  Â· rintro x hx t' âŸ¨ht', htâŸ©
    obtain âŸ¨fs, rflâŸ© : âˆƒ s, t' = zeroLocus ğ’œ s := by rwa [isClosed_iff_zeroLocus] at ht'
    rw [subset_zeroLocus_iff_subset_vanishingIdeal] at ht
    exact Set.Subset.trans ht hx
  Â· rw [(isClosed_zeroLocus _ _).closure_subset_iff]
    exact subset_zeroLocus_vanishingIdeal ğ’œ t

theorem vanishingIdeal_closure (t : Set (ProjectiveSpectrum ğ’œ)) :
    vanishingIdeal (closure t) = vanishingIdeal t := by
  have : (vanishingIdeal (zeroLocus ğ’œ (vanishingIdeal t))).toIdeal = _ := (gc_ideal ğ’œ).u_l_u_eq_u t
  ext1
  rw [zeroLocus_vanishingIdeal_eq_closure ğ’œ t] at this
  exact this

section BasicOpen

/-- `basicOpen r` is the open subset containing all prime ideals not containing `r`. -/
def basicOpen (r : A) : TopologicalSpace.Opens (ProjectiveSpectrum ğ’œ) where
  carrier := { x | r âˆ‰ x.asHomogeneousIdeal }
  is_open' := âŸ¨{r}, Set.ext fun _ => Set.singleton_subset_iff.trans <| Classical.not_not.symmâŸ©

@[simp]
theorem mem_basicOpen (f : A) (x : ProjectiveSpectrum ğ’œ) :
    x âˆˆ basicOpen ğ’œ f â†” f âˆ‰ x.asHomogeneousIdeal :=
  Iff.rfl

theorem mem_coe_basicOpen (f : A) (x : ProjectiveSpectrum ğ’œ) :
    x âˆˆ (â†‘(basicOpen ğ’œ f) : Set (ProjectiveSpectrum ğ’œ)) â†” f âˆ‰ x.asHomogeneousIdeal :=
  Iff.rfl

theorem isOpen_basicOpen {a : A} : IsOpen (basicOpen ğ’œ a : Set (ProjectiveSpectrum ğ’œ)) :=
  (basicOpen ğ’œ a).isOpen

@[simp]
theorem basicOpen_eq_zeroLocus_compl (r : A) :
    (basicOpen ğ’œ r : Set (ProjectiveSpectrum ğ’œ)) = (zeroLocus ğ’œ {r})á¶œ :=
  Set.ext fun x => by simp only [Set.mem_compl_iff, mem_zeroLocus, Set.singleton_subset_iff]; rfl

@[simp]
theorem basicOpen_one : basicOpen ğ’œ (1 : A) = âŠ¤ :=
  TopologicalSpace.Opens.ext <| by simp

@[simp]
theorem basicOpen_zero : basicOpen ğ’œ (0 : A) = âŠ¥ :=
  TopologicalSpace.Opens.ext <| by simp

theorem basicOpen_mul (f g : A) : basicOpen ğ’œ (f * g) = basicOpen ğ’œ f âŠ“ basicOpen ğ’œ g :=
  TopologicalSpace.Opens.ext <| by simp [zeroLocus_singleton_mul]

theorem basicOpen_mul_le_left (f g : A) : basicOpen ğ’œ (f * g) â‰¤ basicOpen ğ’œ f := by
  rw [basicOpen_mul ğ’œ f g]
  exact inf_le_left

theorem basicOpen_mul_le_right (f g : A) : basicOpen ğ’œ (f * g) â‰¤ basicOpen ğ’œ g := by
  rw [basicOpen_mul ğ’œ f g]
  exact inf_le_right

@[simp]
theorem basicOpen_pow (f : A) (n : â„•) (hn : 0 < n) : basicOpen ğ’œ (f ^ n) = basicOpen ğ’œ f :=
  TopologicalSpace.Opens.ext <| by simpa using zeroLocus_singleton_pow ğ’œ f n hn

theorem basicOpen_eq_union_of_projection (f : A) :
    basicOpen ğ’œ f = â¨† i : â„•, basicOpen ğ’œ (GradedAlgebra.proj ğ’œ i f) :=
  TopologicalSpace.Opens.ext <|
    Set.ext fun z => by
      rw [mem_coe_basicOpen, mem_coe, iSup, TopologicalSpace.Opens.mem_sSup]
      constructor <;> intro hz
      Â· rcases show âˆƒ i, GradedAlgebra.proj ğ’œ i f âˆ‰ z.asHomogeneousIdeal by
          contrapose! hz with H
          classical
          rw [â† DirectSum.sum_support_decompose ğ’œ f]
          apply Ideal.sum_mem _ fun i _ => H i with âŸ¨i, hiâŸ©
        exact âŸ¨basicOpen ğ’œ (GradedAlgebra.proj ğ’œ i f), âŸ¨i, rflâŸ©, by rwa [mem_basicOpen]âŸ©
      Â· obtain âŸ¨_, âŸ¨i, rflâŸ©, hzâŸ© := hz
        exact fun rid => hz (z.1.2 i rid)

theorem isTopologicalBasis_basic_opens :
    TopologicalSpace.IsTopologicalBasis
      (Set.range fun r : A => (basicOpen ğ’œ r : Set (ProjectiveSpectrum ğ’œ))) := by
  apply TopologicalSpace.isTopologicalBasis_of_isOpen_of_nhds
  Â· rintro _ âŸ¨r, rflâŸ©
    exact isOpen_basicOpen ğ’œ
  Â· rintro p U hp âŸ¨s, hsâŸ©
    rw [â† compl_compl U, Set.mem_compl_iff, â† hs, mem_zeroLocus, Set.not_subset] at hp
    obtain âŸ¨f, hfs, hfpâŸ© := hp
    refine âŸ¨basicOpen ğ’œ f, âŸ¨f, rflâŸ©, hfp, ?_âŸ©
    rw [â† Set.compl_subset_compl, â† hs, basicOpen_eq_zeroLocus_compl, compl_compl]
    exact zeroLocus_anti_mono ğ’œ (Set.singleton_subset_iff.mpr hfs)

end BasicOpen

section Order

/-!
## The specialization order

We endow `ProjectiveSpectrum ğ’œ` with a partial order,
where `x â‰¤ y` if and only if `y âˆˆ closure {x}`.
-/


instance : PartialOrder (ProjectiveSpectrum ğ’œ) :=
  PartialOrder.lift asHomogeneousIdeal fun âŸ¨_, _, _âŸ© âŸ¨_, _, _âŸ© => by simp only [mk.injEq, imp_self]

@[simp]
theorem as_ideal_le_as_ideal (x y : ProjectiveSpectrum ğ’œ) :
    x.asHomogeneousIdeal â‰¤ y.asHomogeneousIdeal â†” x â‰¤ y :=
  Iff.rfl

@[simp]
theorem as_ideal_lt_as_ideal (x y : ProjectiveSpectrum ğ’œ) :
    x.asHomogeneousIdeal < y.asHomogeneousIdeal â†” x < y :=
  Iff.rfl

theorem le_iff_mem_closure (x y : ProjectiveSpectrum ğ’œ) :
    x â‰¤ y â†” y âˆˆ closure ({x} : Set (ProjectiveSpectrum ğ’œ)) := by
  rw [â† as_ideal_le_as_ideal, â† zeroLocus_vanishingIdeal_eq_closure, mem_zeroLocus,
    vanishingIdeal_singleton]
  simp only [as_ideal_le_as_ideal, coe_subset_coe]

end Order

end ProjectiveSpectrum
