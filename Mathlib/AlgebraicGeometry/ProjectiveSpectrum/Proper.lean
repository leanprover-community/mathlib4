/-
Copyright (c) 2024 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patience Ablett, Kevin Buzzard, Harald Carlens, Wayne Ng Kwing King, Michael SchlÃ¶ÃŸer,
  Justus Springer, Andrew Yang, Jujian Zhang
-/
import Mathlib.AlgebraicGeometry.ProjectiveSpectrum.Basic
import Mathlib.AlgebraicGeometry.ValuativeCriterion

/-!
# Properness of `Proj A`

We show that `Proj ğ’œ` is proper over `Spec ğ’œâ‚€`.

## Notes
This contribution was created as part of the Durham Computational Algebraic Geometry Workshop

-/

namespace AlgebraicGeometry.Proj

variable {R A : Type*}
variable [CommRing R] [CommRing A] [Algebra R A]
variable (ğ’œ : â„• â†’ Submodule R A)
variable [GradedAlgebra ğ’œ]

open Scheme CategoryTheory Limits pullback HomogeneousLocalization

section IsSeparated

lemma lift_awayMapâ‚_awayMapâ‚_surjective {d e : â„•} {f : A} (hf : f âˆˆ ğ’œ d)
    {g : A} (hg : g âˆˆ ğ’œ e) {x : A} (hx : x = f * g) (hd : 0 < d) :
    Function.Surjective
      (Algebra.TensorProduct.lift (awayMapâ‚ ğ’œ hg hx) (awayMapâ‚ ğ’œ hf (hx.trans (mul_comm _ _)))
        (fun _ _ â†¦ .all _ _)) := by
  intro z
  obtain âŸ¨âŸ¨n, âŸ¨a, haâŸ©, âŸ¨b, hb'âŸ©, âŸ¨j, (rfl : _ = b)âŸ©âŸ©, rflâŸ© := mk_surjective z
  by_cases hfg : (f * g) ^ j = 0
  Â· use 0
    have := HomogeneousLocalization.subsingleton ğ’œ (x := Submonoid.powers x) âŸ¨j, hx â–¸ hfgâŸ©
    exact this.elim _ _
  have : n = j * (d + e) := by
    apply DirectSum.degree_eq_of_mem_mem ğ’œ hb'
    convert SetLike.pow_mem_graded _ _ using 2
    Â· infer_instance
    Â· exact hx â–¸ SetLike.mul_mem_graded hf hg
    Â· exact hx â–¸ hfg
  let x0 : NumDenSameDeg ğ’œ (.powers f) :=
  { deg := j * (d * (e + 1))
    num := âŸ¨a * g ^ (j * (d - 1)), by
      convert SetLike.mul_mem_graded ha (SetLike.pow_mem_graded _ hg) using 2
      rw [this]
      cases d
      Â· contradiction
      Â· simp; ringâŸ©
    den := âŸ¨f ^ (j * (e + 1)), by convert SetLike.pow_mem_graded _ hf using 2; ringâŸ©
    den_mem := âŸ¨_,rflâŸ© }
  let y0 : NumDenSameDeg ğ’œ (.powers g) :=
  { deg := j * (d * e)
    num := âŸ¨f ^ (j * e), by convert SetLike.pow_mem_graded _ hf using 2; ringâŸ©
    den := âŸ¨g ^ (j * d), by convert SetLike.pow_mem_graded _ hg using 2; ringâŸ©
    den_mem := âŸ¨_,rflâŸ© }
  use mk x0 âŠ—â‚œ mk y0
  ext
  simp only [Algebra.TensorProduct.lift_tmul, awayMapâ‚_apply, val_mul,
    val_awayMap_mk, Localization.mk_mul, val_mk, x0, y0]
  rw [Localization.mk_eq_mk_iff, Localization.r_iff_exists]
  use 1
  simp only [OneMemClass.coe_one, one_mul, Submonoid.mk_mul_mk]
  cases d
  Â· contradiction
  Â· simp only [hx, add_tsub_cancel_right]
    ring

open TensorProduct in
instance isSeparated : IsSeparated (toSpecZero ğ’œ) := by
  refine âŸ¨IsLocalAtTarget.of_openCover (Pullback.openCoverOfLeftRight
    (affineOpenCover ğ’œ).openCover (affineOpenCover ğ’œ).openCover _ _) ?_âŸ©
  intro âŸ¨i, jâŸ©
  dsimp [Scheme, Cover.pullbackHom]
  refine (MorphismProperty.cancel_left_of_respectsIso (P := @IsClosedImmersion)
    (f := (pullbackDiagonalMapIdIso ..).inv) _).mp ?_
  let eâ‚ : pullback ((affineOpenCover ğ’œ).map i â‰« toSpecZero ğ’œ)
        ((affineOpenCover ğ’œ).map j â‰« toSpecZero ğ’œ) â‰…
        Spec(TensorProduct (ğ’œ 0) (Away ğ’œ i.2) (Away ğ’œ j.2)) := by
    refine pullback.congrHom ?_ ?_ â‰ªâ‰« pullbackSpecIso (ğ’œ 0) (Away ğ’œ i.2) (Away ğ’œ j.2)
    Â· simp [affineOpenCover, openCoverOfISupEqTop, awayÎ¹_toSpecZero]; rfl
    Â· simp [affineOpenCover, openCoverOfISupEqTop, awayÎ¹_toSpecZero]; rfl
  let eâ‚‚ : pullback ((affineOpenCover ğ’œ).map i) ((affineOpenCover ğ’œ).map j) â‰…
        Spec(Away ğ’œ (i.2 * j.2)) :=
    pullbackAwayÎ¹Iso ğ’œ _ _ _ _ rfl
  rw [â† MorphismProperty.cancel_right_of_respectsIso (P := @IsClosedImmersion) _ eâ‚.hom,
    â† MorphismProperty.cancel_left_of_respectsIso (P := @IsClosedImmersion) eâ‚‚.inv]
  let F : Away ğ’œ i.2.1 âŠ—[ğ’œ 0] Away ğ’œ j.2.1 â†’+* Away ğ’œ (i.2.1 * j.2.1) :=
    (Algebra.TensorProduct.lift (awayMapâ‚ ğ’œ j.2.2 rfl) (awayMapâ‚ ğ’œ i.2.2 (mul_comm _ _))
      (fun _ _ â†¦ .all _ _)).toRingHom
  have : Function.Surjective F := lift_awayMapâ‚_awayMapâ‚_surjective ğ’œ i.2.2 j.2.2 rfl i.1.2
  convert IsClosedImmersion.spec_of_surjective
    (CommRingCat.ofHom (R := Away ğ’œ i.2.1 âŠ—[ğ’œ 0] Away ğ’œ j.2.1) F) this using 1
  rw [â† cancel_mono (pullbackSpecIso ..).inv]
  apply pullback.hom_ext
  Â· simp only [Iso.trans_hom, congrHom_hom, Category.assoc, Iso.hom_inv_id, Category.comp_id,
      limit.lift_Ï€, PullbackCone.mk_pt, PullbackCone.mk_Ï€_app, eâ‚‚, eâ‚,
      pullbackDiagonalMapIdIso_inv_snd_fst, pullbackSpecIso_inv_fst,
      â† Spec.map_comp]
    erw [pullbackAwayÎ¹Iso_inv_fst]
    congr 1
    ext x : 2
    exact DFunLike.congr_fun (Algebra.TensorProduct.lift_comp_includeLeft
      (awayMapâ‚ ğ’œ j.2.2 rfl) (awayMapâ‚ ğ’œ i.2.2 (mul_comm _ _)) (fun _ _ â†¦ .all _ _)).symm x
  Â· simp only [Iso.trans_hom, congrHom_hom, Category.assoc, Iso.hom_inv_id, Category.comp_id,
      limit.lift_Ï€, PullbackCone.mk_pt, PullbackCone.mk_Ï€_app,
      pullbackDiagonalMapIdIso_inv_snd_snd, pullbackSpecIso_inv_snd, â†
      Spec.map_comp, eâ‚‚, eâ‚]
    erw [pullbackAwayÎ¹Iso_inv_snd]
    congr 1
    ext x : 2
    exact DFunLike.congr_fun (Algebra.TensorProduct.lift_comp_includeRight
      (awayMapâ‚ ğ’œ j.2.2 rfl) (awayMapâ‚ ğ’œ i.2.2 (mul_comm _ _)) (fun _ _ â†¦ .all _ _)).symm x

@[stacks 01MC]
instance : Scheme.IsSeparated (Proj ğ’œ) :=
  (HasAffineProperty.iff_of_isAffine (P := @IsSeparated)).mp (isSeparated ğ’œ)

end IsSeparated

section LocallyOfFiniteType

instance [Algebra.FiniteType (ğ’œ 0) A] : LocallyOfFiniteType (Proj.toSpecZero ğ’œ) := by
  obtain âŸ¨x, hx, hx'âŸ© := GradedAlgebra.exists_finset_adjoin_eq_top_and_homogeneous_ne_zero ğ’œ
  choose d hd hxd using hx'
  rw [IsLocalAtSource.iff_of_iSup_eq_top (P := @LocallyOfFiniteType) _
    (Proj.iSup_basicOpen_eq_top' ğ’œ (Î¹ := x) (â†‘) (fun i â†¦ âŸ¨_, hxd _ i.2âŸ©) (by simpa using hx))]
  intro i
  rw [â† MorphismProperty.cancel_left_of_respectsIso (P := @LocallyOfFiniteType)
    (Proj.basicOpenIsoSpec ğ’œ i (hxd _ i.2) (hd _ i.2).bot_lt).inv, â† Category.assoc, â† Proj.awayÎ¹,
    Proj.awayÎ¹_toSpecZero, HasRingHomProperty.Spec_iff (P := @LocallyOfFiniteType)]
  exact HomogeneousLocalization.Away.finiteType _ _ (hxd _ i.2)

end LocallyOfFiniteType

section QuasiCompact

instance [Algebra.FiniteType (ğ’œ 0) A] : QuasiCompact (Proj.toSpecZero ğ’œ) := by
  rw [HasAffineProperty.iff_of_isAffine (P := @QuasiCompact)]
  obtain âŸ¨x, hx, hx'âŸ© := GradedAlgebra.exists_finset_adjoin_eq_top_and_homogeneous_ne_zero ğ’œ
  choose d hd hxd using hx'
  have H (i : x) : IsCompact (Proj.basicOpen ğ’œ i).1 := by
    rw [â† Proj.opensRange_awayÎ¹ _ _ (hxd _ i.2) (hd _ i.2).bot_lt]
    exact isCompact_range (Proj.awayÎ¹ _ _ (hxd _ i.2) (hd _ i.2).bot_lt).continuous
  have := congr($(Proj.iSup_basicOpen_eq_top' ğ’œ
    (Î¹ := x) (â†‘) (fun i â†¦ âŸ¨_, hxd _ i.2âŸ©) (by simpa using hx)).1)
  simp only [TopologicalSpace.Opens.iSup_mk, TopologicalSpace.Opens.carrier_eq_coe,
    TopologicalSpace.Opens.coe_mk, TopologicalSpace.Opens.coe_top] at this
  rw [â† isCompact_univ_iff, â† this]
  exact isCompact_iUnion H

end QuasiCompact

section UniversallyClosed

open ValuationRing in
/--
Let `ğ’œ` be a graded ring generated over `ğ’œâ‚€` by finitely many homogeneous elements.
Suppose we have the following diagram for some homogeneous `x`
with `O` a valuation ring and `K = Frac(O)`.
```
    Ï†
K â†--- ğ’œ_{(x)}
â†‘       â†‘
|       |
|       |
O â†---- ğ’œâ‚€
    Ï†â‚€
```
Then there exists a lift `Ï†â‚— : ğ’œ_{(xâ‚€)} â†’+* O` for some `xâ‚€`
such that these two diagrams exist and commute.
```
    Ï†'                      Ï†'
K â†--- ğ’œ_{(x xâ‚€)}       K â†--- ğ’œ_{(x xâ‚€)}
â†‘       â†‘                 â†–       â†‘
|       |                 Ï† âŸ     |
|       |                     âŸ   |
O â†---- ğ’œ_{(xâ‚€)}                ğ’œ_{(x)}
    Ï†â‚—
```
This is the underlying algebraic statement of the valuative criterion for `Proj ğ’œ`.
-/
@[stacks 01MF "algebraic part"]
theorem valuativeCriterion_existence_aux
    {O : Type*} [CommRing O] [IsDomain O] [ValuationRing O]
    {K : Type*} [Field K] [Algebra O K] [IsFractionRing O K]
    (Ï†â‚€ : (ğ’œ 0) â†’+* O)
    (Î¹ : Type*) [Finite Î¹] (x : Î¹ â†’ A) (h2 : Algebra.adjoin (ğ’œ 0) (Set.range x) = âŠ¤)
    (j : Î¹) (Ï† : Away ğ’œ (x j) â†’+* K)
    (hcomm : (algebraMap O K).comp Ï†â‚€ = Ï†.comp (fromZeroRingHom ğ’œ _))
    (d : Î¹ â†’ â„•) (hdi : âˆ€ i, 0 < d i) (hxdi : âˆ€ i, x i âˆˆ ğ’œ (d i)) :
    âˆƒ (jâ‚€ : Î¹) (Ï†' : Away ğ’œ (x j * x jâ‚€) â†’+* K), Ï†'.comp (awayMap ğ’œ (hxdi jâ‚€) rfl) = Ï† âˆ§
      (Ï†'.comp (awayMap ğ’œ (hxdi j) (mul_comm (x j) (x jâ‚€)))).range â‰¤ (algebraMap O K).range := by
  classical
  cases nonempty_fintype Î¹
  let Ïˆ (i : Î¹) : ValueGroup O K :=
    valuation O K ((Ï† (Away.isLocalizationElem (hxdi j) (hxdi i))) ^ âˆ k âˆˆ Finset.univ.erase i, d k)
  have : Nonempty Î¹ := âŸ¨jâŸ©
  let Kmax := (Finset.univ.image Ïˆ).max' (by simp)
  have âŸ¨iâ‚€, hi1âŸ© : âˆƒ a, Ïˆ a = Kmax := by simpa using Finset.max'_mem (Finset.univ.image Ïˆ)
  have hiâ‚€ (j) : Ïˆ j â‰¤ Ïˆ iâ‚€ := hi1 â–¸ (Finset.univ.image Ïˆ).le_max' (Ïˆ j) (by simp)
  have hKmax : 0 < Kmax := by
    refine zero_lt_iff.mpr fun hKmax â†¦ ?_
    have (i : _) : Ïˆ i = 0 := le_zero_iff.mp (hKmax â–¸ Finset.le_max' _ _ (by simp))
    simp only [Ïˆ, map_pow, pow_eq_zero_iff', map_eq_zero, ne_eq] at this
    have : Ï† 1 = 0 := by convert (this j).1; ext; simp
    simp only [map_one, one_ne_zero] at this
  letI := (awayMap ğ’œ (f := x j) (hxdi iâ‚€) rfl).toAlgebra
  have := Away.isLocalization_mul (hxdi j) (hxdi iâ‚€) rfl (hdi _).ne'
  have hunit : IsUnit (Ï† (Away.isLocalizationElem (hxdi j) (hxdi iâ‚€))) := isUnit_iff_ne_zero.mpr
    fun rid â†¦ hKmax.ne' (.symm (by simpa [Ïˆ, rid, Finset.prod_eq_zero_iff, (hdi _).ne'] using hi1))
  let Ï†' := IsLocalization.Away.lift (S := Away ğ’œ (x j * x iâ‚€)) _ hunit
  have hÏ†'1 (s) : Ï†' (awayMap ğ’œ (hxdi iâ‚€) rfl s) = Ï† s := IsLocalization.Away.lift_eq _ hunit s
  use iâ‚€, Ï†', IsLocalization.Away.lift_comp ..
  rintro _ âŸ¨sx, rflâŸ©
  rw [RingHom.mem_range, â† ValuationRing.mem_integer_iff, Valuation.mem_integer_iff]
  have := (Away.span_mk_prod_pow_eq_top (hxdi iâ‚€) x h2 d hxdi).ge (Submodule.mem_top (x := sx))
  induction this using Submodule.span_induction with
  | zero => simp
  | add x y hx hy hhx hhy =>
    simp only [RingHom.coe_comp, Function.comp_apply, map_add, ge_iff_le]
    exact ((valuation O K).map_add _ _).trans <| sup_le_iff.mpr âŸ¨hhx, hhyâŸ©
  | smul a xâ‚€ hx hx1 =>
    simp only [Algebra.smul_def, RingHom.coe_comp, Function.comp_apply, map_mul, ge_iff_le]
    refine mul_le_one' ?_ hx1
    rw [RingHom.algebraMap_toAlgebra, awayMap_fromZeroRingHom ğ’œ (hxdi j) (mul_comm (x j) (x iâ‚€)),
      â† awayMap_fromZeroRingHom ğ’œ (hxdi iâ‚€) rfl a, hÏ†'1]
    change valuation O K (Ï†.comp (fromZeroRingHom ğ’œ (.powers (x j))) a) â‰¤ 1
    simp only [â† hcomm, RingHom.coe_comp, Function.comp_apply, â† Valuation.mem_integer_iff,
      IsFractionRing.coe_inj, exists_eq, mem_integer_iff]
  | mem x1 h =>
    obtain âŸ¨a, ai, hai, rflâŸ© := h
    simp only [smul_eq_mul] at hai
    have H : (âˆ i, x i ^ ai i) * x iâ‚€ ^ (a * (d j - 1)) âˆˆ ğ’œ ((a * d iâ‚€) â€¢ d j) := by
      convert SetLike.mul_mem_graded (SetLike.prod_pow_mem_graded ğ’œ d x ai fun _ _ â†¦ hxdi _)
        (SetLike.pow_mem_graded (a * (d j - 1)) (hxdi iâ‚€)) using 2
      simp only [smul_eq_mul, hai]
      cases h : d j
      Â· cases (hdi j).ne' h
      Â· simp only [add_tsub_cancel_right]; ring
    suffices valuation O K (Ï† (Away.mk ğ’œ (hxdi j) _ _ H) /
          Ï† (Away.isLocalizationElem (hxdi j) (hxdi iâ‚€)) ^ a) â‰¤ 1 by
      convert this
      rw [eq_div_iff (pow_ne_zero _ hunit.ne_zero), â† hÏ†'1, â† hÏ†'1, RingHom.comp_apply,
        â† map_pow, â† map_mul]
      congr
      ext
      simp only [awayMap_mk, Away.val_mk, val_pow, Localization.mk_pow, Localization.mk_mul,
        Localization.mk_eq_mk_iff, Localization.r_iff_exists, val_mul]
      use 1
      simp only [OneMemClass.coe_one, one_mul, Submonoid.coe_mul, SubmonoidClass.coe_pow]
      cases h : d j
      Â· cases (hdi j).ne' h
      Â· rw [Nat.add_sub_cancel]; ring
    rw [map_divâ‚€, div_le_iffâ‚€ ((pow_pos ((Valuation.pos_iff _).mpr hunit.ne_zero) _).trans_eq
      (Valuation.map_pow _ _ _).symm), one_mul, â† pow_le_pow_iff_leftâ‚€ zero_le' zero_le'
        (mul_pos (hdi j) (Finset.prod_pos fun i _ => hdi i)).ne.symm]
    calc
      _ = (âˆ i, Ïˆ i ^ (d i * ai i)) * Ïˆ iâ‚€ ^ (d iâ‚€ * a * (d j - 1)) := by
          simp only [Ïˆ, â† map_pow, â† map_prod, â† map_mul]
          congr 2
          apply (show Function.Injective (algebraMap (Away ğ’œ (x j)) (Localization.Away (x j)))
            from val_injective _)
          simp only [map_pow, map_prod, map_mul]
          simp only [HomogeneousLocalization.algebraMap_apply, Away.val_mk, Localization.mk_pow,
            Localization.mk_prod, Localization.mk_mul]
          rw [Localization.mk_eq_mk_iff, Localization.r_iff_exists]
          use 1
          simp only [OneMemClass.coe_one, â† pow_mul, Submonoid.coe_mul,
            SubmonoidClass.coe_finset_prod, one_mul]
          simp_rw [â† mul_assoc, Finset.prod_erase_mul _ d (h := Finset.mem_univ _), mul_assoc,
            â† mul_assoc (Finset.prod ..), Finset.prod_erase_mul _ d (h := Finset.mem_univ _),
            SubmonoidClass.coe_pow, â† pow_mul, Finset.prod_pow_eq_pow_sum,
            â† pow_add, mul_pow, â† Finset.prod_pow, â† pow_mul]
          congr 3
          Â· simp only [mul_comm _ (âˆ i, d i), mul_assoc, mul_left_comm _ (âˆ i, d i),
              mul_comm (d _) (ai _), â† Finset.mul_sum, hai]
            cases h : d j
            Â· cases (hdi j).ne' h
            Â· simp; ring
          Â· ext i; congr 1; ring
          Â· ring
      _ â‰¤ (âˆ i : Î¹, Ïˆ iâ‚€ ^ (d i * ai i)) * Ïˆ iâ‚€ ^ (d iâ‚€ * a * (d j - 1)) :=
          mul_le_mul_right' (Finset.prod_le_prod' fun i a â†¦ pow_le_pow_leftâ‚€ zero_le' (hiâ‚€ i) _) _
      _ = Ïˆ iâ‚€ ^ (d iâ‚€ * a * d j) := by
          rw [Finset.prod_pow_eq_pow_sum, â† pow_add]
          simp_rw [mul_comm (d _) (ai _), hai]
          cases h : d j
          Â· cases (hdi j).ne' h
          Â· simp; ring_nf
      _ = valuation O K ((Ï† _) ^ a) ^ (d j * âˆ i, d i) := by
          Â· simp only [Ïˆ, â† map_pow]
            congr 2
            rw [â† pow_mul, â† pow_mul, â† mul_assoc, â† mul_assoc, â† mul_assoc,
              Finset.univ.prod_erase_mul d (h := Finset.mem_univ _),
              mul_comm _ a, mul_right_comm]

@[stacks 01MF]
lemma valuativeCriterion_existence [Algebra.FiniteType (ğ’œ 0) A] :
    ValuativeCriterion.Existence (Proj.toSpecZero ğ’œ) := by
  rintro âŸ¨O, K, iâ‚, iâ‚‚, wâŸ©
  obtain âŸ¨x, hx, hx'âŸ© := GradedAlgebra.exists_finset_adjoin_eq_top_and_homogeneous_ne_zero ğ’œ
  choose d hd hxd using hx'
  have : iâ‚.base (IsLocalRing.closedPoint K) âˆˆ (âŠ¤ : (Proj ğ’œ).Opens) := trivial
  rw [â† Proj.iSup_basicOpen_eq_top' ğ’œ (Î¹ := x) (â†‘) (fun i â†¦ âŸ¨_, hxd _ i.2âŸ©) (by simpa using hx),
    TopologicalSpace.Opens.mem_iSup] at this
  obtain âŸ¨i, hiâŸ© := this
  have : Set.range iâ‚.base âŠ† (Proj.awayÎ¹ ğ’œ _ (hxd i i.2) (hd i i.2).bot_lt).opensRange := by
    rw [Proj.opensRange_awayÎ¹]
    rintro _ âŸ¨x, rflâŸ©
    obtain rfl := Subsingleton.elim x (IsLocalRing.closedPoint K)
    exact hi
  let Ï† : Spec(K) âŸ¶ _ := IsOpenImmersion.lift _ _ this
  have H : Spec.preimage iâ‚‚ â‰« CommRingCat.ofHom (algebraMap O K) =
      CommRingCat.ofHom (fromZeroRingHom ğ’œ _) â‰« Spec.preimage Ï† := by
    apply Spec.map_injective
    simp only [Spec.map_comp, Spec.map_preimage, â† w.w]
    rw [â† Proj.awayÎ¹_toSpecZero, IsOpenImmersion.lift_fac_assoc]
  obtain âŸ¨iâ‚€, Ï†', hÏ†, hÏ†'âŸ© :=
    valuativeCriterion_existence_aux ğ’œ (Spec.preimage iâ‚‚).hom x (â†‘) (by simpa using hx) i
      (O := O) (K := K) (Spec.preimage Ï†).hom congr(($H).hom)
      (fun i â†¦ d _ i.2) (fun i â†¦ (hd _ i.2).bot_lt) (fun i â†¦ hxd _ i.2)
  let e : O â‰ƒ+* (algebraMap O K).range :=
    (AlgEquiv.ofInjective (Algebra.ofId O K) (IsFractionRing.injective _ _)).toRingEquiv
  let Ï†'' := e.symm.toRingHom.comp ((Subring.inclusion hÏ†').comp (RingHom.rangeRestrict _))
  have hÏ†'' : (algebraMap O K).comp Ï†'' = Ï†'.comp (awayMap ğ’œ (hxd _ i.2) (mul_comm _ _)) := by
    ext x
    exact congr_arg Subtype.val (e.apply_symm_apply _)
  refine âŸ¨âŸ¨Spec.map (CommRingCat.ofHom Ï†'') â‰« Proj.awayÎ¹ ğ’œ _ (hxd _ iâ‚€.2) (hd _ _).bot_lt, ?_, ?_âŸ©âŸ©
  Â· rw [â† Spec.map_comp_assoc]
    convert IsOpenImmersion.lift_fac _ _ this using 1
    change _ = Ï† â‰« _
    rw [â† Spec.map_preimage Ï†, â† CommRingCat.ofHom_hom (Spec.preimage Ï†), â† hÏ†,
      â† CommRingCat.ofHom_comp]
    simp [hÏ†'', SpecMap_awayMap_awayÎ¹, add_comm]
  Â· simp only [Category.assoc, Proj.awayÎ¹_toSpecZero, â† Spec.map_comp]
    conv_rhs => rw [â† Spec.map_preimage iâ‚‚]
    congr 1
    ext x
    apply IsFractionRing.injective O K
    refine (DFunLike.congr_fun hÏ†'' (fromZeroRingHom ğ’œ _ _)).trans ?_
    simp only [RingHom.coe_comp, Function.comp_apply]
    rw [awayMap_fromZeroRingHom, â† awayMap_fromZeroRingHom ğ’œ _ rfl, â† RingHom.comp_apply, hÏ†]
    exact congr($(H.symm) x)

instance [Algebra.FiniteType (ğ’œ 0) A] : UniversallyClosed (Proj.toSpecZero ğ’œ) := by
  rw [UniversallyClosed.eq_valuativeCriterion]
  exact âŸ¨valuativeCriterion_existence ğ’œ, inferInstanceâŸ©

end UniversallyClosed

instance [Algebra.FiniteType (ğ’œ 0) A] : IsProper (Proj.toSpecZero ğ’œ) where

end AlgebraicGeometry.Proj
