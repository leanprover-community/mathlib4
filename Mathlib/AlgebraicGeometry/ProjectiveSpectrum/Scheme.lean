/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import Mathlib.AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf
import Mathlib.AlgebraicGeometry.Spec
import Mathlib.RingTheory.GradedAlgebra.Radical

#align_import algebraic_geometry.projective_spectrum.scheme from "leanprover-community/mathlib"@"d39590fc8728fbf6743249802486f8c91ffe07bc"

/-!
# Proj as a scheme

This file is to prove that `Proj` is a scheme.

## Notation

* `Proj`      : `Proj` as a locally ringed space
* `Proj.T`    : the underlying topological space of `Proj`
* `Proj| U`   : `Proj` restricted to some open set `U`
* `Proj.T| U` : the underlying topological space of `Proj` restricted to open set `U`
* `pbo f`     : basic open set at `f` in `Proj`
* `Spec`      : `Spec` as a locally ringed space
* `Spec.T`    : the underlying topological space of `Spec`
* `sbo g`     : basic open set at `g` in `Spec`
* `A‚Å∞_x`      : the degree zero part of localized ring `A‚Çì`

## Implementation

In `AlgebraicGeometry/ProjectiveSpectrum/StructureSheaf.lean`, we have given `Proj` a
structure sheaf so that `Proj` is a locally ringed space. In this file we will prove that `Proj`
equipped with this structure sheaf is a scheme. We achieve this by using an affine cover by basic
open sets in `Proj`, more specifically:

1. We prove that `Proj` can be covered by basic open sets at homogeneous element of positive degree.
2. We prove that for any homogeneous element `f : A` of positive degree `m`, `Proj.T | (pbo f)` is
    homeomorphic to `Spec.T A‚Å∞_f`:
  - forward direction `toSpec`:
    for any `x : pbo f`, i.e. a relevant homogeneous prime ideal `x`, send it to
    `A‚Å∞_f ‚à© span {g / 1 | g ‚àà x}` (see `ProjIsoSpecTopComponent.IoSpec.carrier`). This ideal is
    prime, the proof is in `ProjIsoSpecTopComponent.ToSpec.toFun`. The fact that this function
    is continuous is found in `ProjIsoSpecTopComponent.toSpec`
  - backward direction `fromSpec`:
    for any `q : Spec A‚Å∞_f`, we send it to `{a | ‚àÄ i, a·µ¢·µê/f‚Å± ‚àà q}`; we need this to be a
    homogeneous prime ideal that is relevant.
    * This is in fact an ideal, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal`;
    * This ideal is also homogeneous, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal.homogeneous`;
    * This ideal is relevant, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.relevant`;
    * This ideal is prime, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.prime`.
    Hence we have a well defined function `Spec.T A‚Å∞_f ‚Üí Proj.T | (pbo f)`, this function is called
    `ProjIsoSpecTopComponent.FromSpec.toFun`. But to prove the continuity of this function, we need
    to prove `fromSpec ‚àò toSpec` and `toSpec ‚àò fromSpec` are both identities; these are achieved in
    `ProjIsoSpecTopComponent.fromSpec_toSpec` and `ProjIsoSpecTopComponent.toSpec_fromSpec`.

## Main Definitions and Statements

For a homogeneous element `f` of degree `m`
* `ProjIsoSpecTopComponent.toSpec`: the continuous map between `Proj.T| pbo f` and `Spec.T A‚Å∞_f`
  defined by sending `x : Proj| (pbo f)` to `A‚Å∞_f ‚à© span {g / 1 | g ‚àà x}`. We also denote this map
  as `œà`.
* `ProjIsoSpecTopComponent.ToSpec.preimage_eq`: for any `a: A`, if `a/f^m` has degree zero,
  then the preimage of `sbo a/f^m` under `to_Spec f` is `pbo f ‚à© pbo a`.

If we further assume `m` is positive
* `ProjIsoSpecTopComponent.fromSpec`: the continuous map between `Spec.T A‚Å∞_f` and `Proj.T| pbo f`
  defined by sending `q` to `{a | a·µ¢·µê/f‚Å± ‚àà q}` where `a·µ¢` is the `i`-th coordinate of `a`.
  We also denote this map as `œÜ`
* `projIsoSpecTopComponent`: the homeomorphism `Proj.T| pbo f ‚âÖ Spec.T A‚Å∞_f` obtained by `œÜ` and
  `œà`.
## Reference
* [Robin Hartshorne, *Algebraic Geometry*][Har77]: Chapter II.2 Proposition 2.5
-/


noncomputable section

set_option linter.uppercaseLean3 false

namespace AlgebraicGeometry

open scoped DirectSum BigOperators Pointwise BigOperators

open DirectSum SetLike.GradedMonoid Localization

open Finset hiding mk_zero

variable {R A : Type*}
variable [CommRing R] [CommRing A] [Algebra R A]
variable (ùíú : ‚Ñï ‚Üí Submodule R A)
variable [GradedAlgebra ùíú]

open TopCat TopologicalSpace

open CategoryTheory Opposite

open ProjectiveSpectrum.StructureSheaf

-- Porting note: currently require lack of hygiene to use in variable declarations
-- maybe all make into notation3?
set_option hygiene false
/-- `Proj` as a locally ringed space -/
local notation3 "Proj" => Proj.toLocallyRingedSpace ùíú

/-- The underlying topological space of `Proj` -/
local notation3 "Proj.T" => PresheafedSpace.carrier <| SheafedSpace.toPresheafedSpace
  <| LocallyRingedSpace.toSheafedSpace <| Proj.toLocallyRingedSpace ùíú

/-- `Proj` restrict to some open set -/
macro "Proj| " U:term : term =>
  `((Proj.toLocallyRingedSpace ùíú).restrict (Opens.openEmbedding (X := Proj.T) ($U : Opens Proj.T)))

/-- the underlying topological space of `Proj` restricted to some open set -/
local notation "Proj.T| " U => PresheafedSpace.carrier <| SheafedSpace.toPresheafedSpace
  <| LocallyRingedSpace.toSheafedSpace
    <| (LocallyRingedSpace.restrict Proj (Opens.openEmbedding (X := Proj.T) (U : Opens Proj.T)))

/-- basic open sets in `Proj` -/
local notation "pbo " x => ProjectiveSpectrum.basicOpen ùíú x

/-- basic open sets in `Spec` -/
local notation "sbo " f => PrimeSpectrum.basicOpen f

/-- `Spec` as a locally ringed space -/
local notation3 "Spec " ring => Spec.locallyRingedSpaceObj (CommRingCat.of ring)

/-- the underlying topological space of `Spec` -/
local notation "Spec.T " ring =>
  (Spec.locallyRingedSpaceObj (CommRingCat.of ring)).toSheafedSpace.toPresheafedSpace.1

local notation3 "A‚Å∞_ " f => HomogeneousLocalization.Away ùíú f

namespace ProjIsoSpecTopComponent

/-
This section is to construct the homeomorphism between `Proj` restricted at basic open set at
a homogeneous element `x` and `Spec A‚Å∞‚Çì` where `A‚Å∞‚Çì` is the degree zero part of the localized
ring `A‚Çì`.
-/
namespace ToSpec

open Ideal

-- This section is to construct the forward direction :
-- So for any `x` in `Proj| (pbo f)`, we need some point in `Spec A‚Å∞_f`, i.e. a prime ideal,
-- and we need this correspondence to be continuous in their Zariski topology.
variable {ùíú} {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m) (x : Proj| (pbo f))

/--
For any `x` in `Proj| (pbo f)`, the corresponding ideal in `Spec A‚Å∞_f`. This fact that this ideal
is prime is proven in `TopComponent.Forward.toFun`-/
def carrier : Ideal (A‚Å∞_ f) :=
  Ideal.comap (algebraMap (A‚Å∞_ f) (Away f))
    (x.val.asHomogeneousIdeal.toIdeal.map (algebraMap A (Away f)))
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec.carrier AlgebraicGeometry.ProjIsoSpecTopComponent.ToSpec.carrier

theorem mem_carrier_iff_exists (z : A‚Å∞_ f) :
    z ‚àà carrier x ‚Üî ‚àÉ y ‚àà x.1.asHomogeneousIdeal, ‚àÉ n, f ^ n ‚Ä¢ z.val = algebraMap _ _ y := by
  rw [carrier, Ideal.mem_comap, isLocalization.mem_map_algebraMap_iff]
  simp [Submonoid.mem_powers_iff, mul_comm z.val, Algebra.smul_def]

theorem MemCarrier.eventually_mem_ideal {z : A‚Å∞_ f} (hz : z ‚àà carrier x) :
    ‚àÄ·∂† n in Filter.atTop, f ^ n ‚Ä¢ z.val ‚àà algebraMap A (Away f) '' x.1.asHomogeneousIdeal := by
  rw [mem_carrier_iff_exists] at hz
  obtain ‚ü®y, hy, n, e‚ü© := hz
  apply Filter.mem_of_superset (Filter.Ici_mem_atTop n)
  rintro n' (hn' : n ‚â§ n')
  refine ‚ü®f ^ (n' - n) * y, Ideal.mul_mem_left _ _ hy, ?_‚ü©
  conv_rhs => rw [‚Üê tsub_add_cancel_of_le hn']
  simp only [Algebra.smul_def, map_pow, ‚Üê e, mul_comm, pow_add, _root_.map_mul, mul_assoc]

theorem MemCarrier.eventually_exists {z : A‚Å∞_ f} (hz : z ‚àà carrier x) :
    ‚àÄ·∂† n in Filter.atTop, ‚àÉ y ‚àà x.1.asHomogeneousIdeal,
      y ‚àà ùíú (n * m) ‚àß f ^ n ‚Ä¢ z.val = algebraMap A (Away f) y := by
  obtain ‚ü®n, ‚ü®x‚ÇÅ, hx‚ÇÅ, e‚ÇÅ‚ü©, x‚ÇÇ, hx‚ÇÇ, e‚ÇÇ‚ü© := ((MemCarrier.eventually_mem_ideal x hz).and
    (HomogeneousLocalization.Away.eventually_smul_mem f_deg z)).exists
  obtain ‚ü®‚ü®_, n', rfl‚ü©, e : f ^ n' * _ = f ^ n' * _‚ü© :=
    IsLocalization.exists_of_eq (M := Submonoid.powers f) (e‚ÇÅ.trans e‚ÇÇ.symm)
  apply Filter.mem_of_superset (Filter.Ici_mem_atTop (n' + n))
  rintro N (hn' : _ ‚â§ N)
  refine ‚ü®f ^ (N - n) * x‚ÇÅ, Ideal.mul_mem_left _ _ hx‚ÇÅ, ?_, ?_‚ü©
  ¬∑ nth_rw 1 [‚Üê tsub_add_cancel_of_le (Nat.le_sub_of_add_le hn')]
    rw [pow_add, mul_assoc, e, ‚Üê mul_assoc, ‚Üê pow_add,
      tsub_add_cancel_of_le (Nat.le_sub_of_add_le hn')]
    nth_rw 2 [‚Üê tsub_add_cancel_of_le ((Nat.le_add_left _ _).trans hn')]
    rw [add_mul]
    exact SetLike.mul_mem_graded (SetLike.pow_mem_graded _ f_deg) hx‚ÇÇ
  ¬∑ rw [_root_.map_mul, e‚ÇÅ, ‚Üê Algebra.smul_def, ‚Üê mul_smul, ‚Üê pow_add,
      tsub_add_cancel_of_le ((Nat.le_add_left _ _).trans hn')]

theorem isPrime_carrier : Ideal.IsPrime (carrier x) := by
  refine Ideal.IsPrime.comap _ (hK := ?_)
  exact IsLocalization.isPrime_of_isPrime_disjoint
    (Submonoid.powers f) _ _ inferInstance
    ((disjoint_powers_iff_not_mem _ (Ideal.IsPrime.isRadical inferInstance)).mpr x.2)

variable (f)

/-- The function between the basic open set `D(f)` in `Proj` to the corresponding basic open set in
`Spec A‚Å∞_f`. This is bundled into a continuous map in `TopComponent.forward`.
-/
@[simps (config := .lemmasOnly)]
def toFun (x : Proj.T| pbo f) : Spec.T A‚Å∞_ f :=
  ‚ü®carrier x, isPrime_carrier x‚ü©
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec.to_fun AlgebraicGeometry.ProjIsoSpecTopComponent.ToSpec.toFun

/-
The preimage of basic open set `D(a/f^n)` in `Spec A‚Å∞_f` under the forward map from `Proj A` to
`Spec A‚Å∞_f` is the basic open set `D(a) ‚à© D(f)` in `Proj A`. This lemma is used to prove that the
forward map is continuous.
-/
theorem preimage_basicOpen (z) :
    toFun f ‚Åª¬π'
      (PrimeSpectrum.basicOpen (R := A‚Å∞_ f) (Quotient.mk'' z) : Set (PrimeSpectrum (A‚Å∞_ f))) =
      Subtype.val ‚Åª¬π' (pbo z.num.1 : Set (ProjectiveSpectrum ùíú)) := by
  classical
  ext1 y
  apply Iff.not
  constructor
  ¬∑ rintro rid
    obtain ‚ü®n, c, hcy, e‚ü© := (MemCarrier.eventually_mem_ideal _ rid).exists
    simp only [HomogeneousLocalization.val_mk'', Localization.mk_eq_mk',
      IsLocalization.smul_mk', IsLocalization.eq_mk'_iff_mul_eq, ‚Üê _root_.map_mul] at e
    obtain ‚ü®‚ü®_, n', rfl‚ü©, hn'‚ü© := IsLocalization.exists_of_eq (M := Submonoid.powers f) e
    simp only [‚Üê mul_assoc, ‚Üê pow_add] at hn'
    refine (y.1.2.mem_or_mem ?_).resolve_left (not_imp_not.mpr (y.1.2.mem_of_pow_mem (n' + n)) y.2)
    rw [‚Üê hn']
    exact Ideal.mul_mem_right _ _ (Ideal.mul_mem_left _ _ hcy)
  ¬∑ intro a_mem_y
    rw [toFun_asIdeal, mem_carrier_iff_exists]
    obtain ‚ü®n, hn‚ü© := z.den_mem
    refine ‚ü®z.num, a_mem_y, n, ?_‚ü©
    simp only [HomogeneousLocalization.val_mk'',
      Localization.mk_eq_mk', IsLocalization.smul_mk', hn]
    exact IsLocalization.mk'_mul_cancel_left _ ‚ü®_, _‚ü©
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec.preimage_eq AlgebraicGeometry.ProjIsoSpecTopComponent.ToSpec.preimage_basicOpen

end ToSpec

section

/-- The continuous function from the basic open set `D(f)` in `Proj`
to the corresponding basic open set in `Spec A‚Å∞_f`. -/
@[simps! (config := .lemmasOnly) apply_asIdeal]
def toSpec (f : A) : (Proj.T| pbo f) ‚ü∂ Spec.T A‚Å∞_ f where
  toFun := ToSpec.toFun f
  continuous_toFun := by
    rw [PrimeSpectrum.isTopologicalBasis_basic_opens.continuous_iff]
    rintro _ ‚ü®x, rfl‚ü©
    obtain ‚ü®x, rfl‚ü© := Quotient.surjective_Quotient_mk'' x
    rw [ToSpec.preimage_basicOpen]
    exact (pbo x.num).2.preimage continuous_subtype_val
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec AlgebraicGeometry.ProjIsoSpecTopComponent.toSpec

variable {ùíú} in
lemma toSpec_preimage_basicOpen {f} (z) :
    toSpec ùíú f ‚Åª¬π'
      (PrimeSpectrum.basicOpen (R := A‚Å∞_ f) (Quotient.mk'' z) : Set (PrimeSpectrum (A‚Å∞_ f))) =
      Subtype.val ‚Åª¬π' (pbo z.num.1 : Set (ProjectiveSpectrum ùíú)) :=
  ToSpec.preimage_basicOpen f z

end

namespace FromSpec

open GradedAlgebra SetLike

open Finset hiding mk_zero

-- Porting note: _root_ doesn't work here
open HomogeneousLocalization

variable {ùíú} {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m)

open Lean Meta Elab Tactic

macro "mem_tac_aux" : tactic =>
  `(tactic| first | exact pow_mem_graded _ (Submodule.coe_mem _) | exact natCast_mem_graded _ _ |
    exact pow_mem_graded _ f_deg)

macro "mem_tac" : tactic =>
  `(tactic| first | mem_tac_aux |
    repeat (all_goals (apply SetLike.GradedMonoid.toGradedMul.mul_mem)); mem_tac_aux)

/-- The function from `Spec A‚Å∞_f` to `Proj|D(f)` is defined by `q ‚Ü¶ {a | a·µ¢·µê/f‚Å± ‚àà q}`, i.e. sending
`q` a prime ideal in `A‚Å∞_f` to the homogeneous prime relevant ideal containing only and all the
elements `a : A` such that for every `i`, the degree 0 element formed by dividing the `m`-th power
of the `i`-th projection of `a` by the `i`-th power of the degree-`m` homogeneous element `f`,
lies in `q`.

The set `{a | a·µ¢·µê/f‚Å± ‚àà q}`
* is an ideal, as proved in `carrier.asIdeal`;
* is homogeneous, as proved in `carrier.asHomogeneousIdeal`;
* is prime, as proved in `carrier.asIdeal.prime`;
* is relevant, as proved in `carrier.relevant`.
-/
def carrier (q : Spec.T A‚Å∞_ f) : Set A :=
  {a | ‚àÄ i, (Quotient.mk'' ‚ü®m * i, ‚ü®proj ùíú i a ^ m, by mem_tac‚ü©,
              ‚ü®f ^ i, by rw [mul_comm]; mem_tac‚ü©, ‚ü®_, rfl‚ü©‚ü© : A‚Å∞_ f) ‚àà q.1}
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier

theorem mem_carrier_iff (q : Spec.T A‚Å∞_ f) (a : A) :
    a ‚àà carrier f_deg q ‚Üî ‚àÄ i, (Quotient.mk'' ‚ü®m * i, ‚ü®proj ùíú i a ^ m, by mem_tac‚ü©,
      ‚ü®f ^ i, by rw [mul_comm]; mem_tac‚ü©, ‚ü®_, rfl‚ü©‚ü© : A‚Å∞_ f) ‚àà q.1 :=
  Iff.rfl
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.mem_carrier_iff AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.mem_carrier_iff

theorem mem_carrier_iff' (q : Spec.T A‚Å∞_ f) (a : A) :
    a ‚àà carrier f_deg q ‚Üî
      ‚àÄ i, (Localization.mk (proj ùíú i a ^ m) ‚ü®f ^ i, ‚ü®i, rfl‚ü©‚ü© : Localization.Away f) ‚àà
          algebraMap (HomogeneousLocalization.Away ùíú f) (Localization.Away f) '' { s | s ‚àà q.1 } :=
  (mem_carrier_iff f_deg q a).trans
    (by
      constructor <;> intro h i <;> specialize h i
      ¬∑ rw [Set.mem_image]; refine' ‚ü®_, h, rfl‚ü©
      ¬∑ rw [Set.mem_image] at h; rcases h with ‚ü®x, h, hx‚ü©
        change x ‚àà q.asIdeal at h
        convert h
        rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'']
        dsimp only [Subtype.coe_mk]; rw [‚Üê hx]; rfl)
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.mem_carrier_iff' AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.mem_carrier_iff'

theorem mem_carrier_iff_of_mem (hm : 0 < m) (q : Spec.T A‚Å∞_ f) (a : A) {n} (hn : a ‚àà ùíú n) :
    a ‚àà carrier f_deg q ‚Üî
      (Quotient.mk'' ‚ü®m * n, ‚ü®a ^ m, pow_mem_graded m hn‚ü©,
        ‚ü®f ^ n, by rw [mul_comm]; mem_tac‚ü©, ‚ü®_, rfl‚ü©‚ü© : A‚Å∞_ f) ‚àà q.asIdeal := by
  trans (Quotient.mk'' ‚ü®m * n, ‚ü®proj ùíú n a ^ m, by mem_tac‚ü©,
    ‚ü®f ^ n, by rw [mul_comm]; mem_tac‚ü©, ‚ü®_, rfl‚ü©‚ü© : A‚Å∞_ f) ‚àà q.asIdeal
  ¬∑ refine ‚ü®fun h ‚Ü¶ h n, fun h i ‚Ü¶ if hi : i = n then hi ‚ñ∏ h else ?_‚ü©
    convert zero_mem q.asIdeal
    apply HomogeneousLocalization.val_injective
    simp only [proj_apply, decompose_of_mem_ne _ hn (Ne.symm hi), zero_pow hm.ne',
      HomogeneousLocalization.val_mk'', mk_zero, HomogeneousLocalization.zero_val]
  ¬∑ simp only [proj_apply, decompose_of_mem_same _ hn]

theorem mem_carrier_iff_of_mem_mul (hm : 0 < m)
    (q : Spec.T A‚Å∞_ f) (a : A) {n} (hn : a ‚àà ùíú (n * m)) :
    a ‚àà carrier f_deg q ‚Üî (Quotient.mk'' ‚ü®m * n, ‚ü®a, mul_comm n m ‚ñ∏ hn‚ü©,
        ‚ü®f ^ n, by rw [mul_comm]; mem_tac‚ü©, ‚ü®_, rfl‚ü©‚ü© : A‚Å∞_ f) ‚àà q.asIdeal := by
  rw [mem_carrier_iff_of_mem f_deg hm q a hn, iff_iff_eq, eq_comm,
    ‚Üê Ideal.IsPrime.pow_mem_iff_mem (Œ± := A‚Å∞_ f) inferInstance m hm]
  congr 1
  apply HomogeneousLocalization.val_injective
  simp only [HomogeneousLocalization.val_mk'', HomogeneousLocalization.pow_val,
    Localization.mk_pow, pow_mul]
  rfl

theorem carrier.add_mem (q : Spec.T A‚Å∞_ f) {a b : A} (ha : a ‚àà carrier f_deg q)
    (hb : b ‚àà carrier f_deg q) : a + b ‚àà carrier f_deg q := by
  refine fun i => (q.2.mem_or_mem ?_).elim id id
  change (Quotient.mk'' ‚ü®_, _, _, _‚ü© : A‚Å∞_ f) ‚àà q.1; dsimp only [Subtype.coe_mk]
  simp_rw [‚Üê pow_add, map_add, add_pow, mul_comm, ‚Üê nsmul_eq_mul]
  let g : ‚Ñï ‚Üí A‚Å∞_ f := fun j => (m + m).choose j ‚Ä¢
      if h2 : m + m < j then (0 : A‚Å∞_ f)
      else
        -- Porting note: inlining `l`, `r` causes a "can't synth HMul A‚Å∞_ f A‚Å∞_ f ?" error
        if h1 : j ‚â§ m then
          letI l : A‚Å∞_ f := Quotient.mk''
            ‚ü®m * i, ‚ü®proj ùíú i a ^ j * proj ùíú i b ^ (m - j), ?_‚ü©,
              ‚ü®_, by rw [mul_comm]; mem_tac‚ü©, ‚ü®i, rfl‚ü©‚ü©
          letI r : A‚Å∞_ f := Quotient.mk''
            ‚ü®m * i, ‚ü®proj ùíú i b ^ m, by mem_tac‚ü©,
              ‚ü®_, by rw [mul_comm]; mem_tac‚ü©, ‚ü®i, rfl‚ü©‚ü©
          l * r
        else
          letI l : A‚Å∞_ f := Quotient.mk''
            ‚ü®m * i, ‚ü®proj ùíú i a ^ m, by mem_tac‚ü©,
              ‚ü®_, by rw [mul_comm]; mem_tac‚ü©, ‚ü®i, rfl‚ü©‚ü©
          letI r : A‚Å∞_ f := Quotient.mk''
            ‚ü®m * i, ‚ü®proj ùíú i a ^ (j - m) * proj ùíú i b ^ (m + m - j), ?_‚ü©,
              ‚ü®_, by rw [mul_comm]; mem_tac‚ü©, ‚ü®i, rfl‚ü©‚ü©
          l * r
  rotate_left
  ¬∑ rw [(_ : m * i = _)]
    -- Porting note: it seems unification with mul_mem is more fiddly reducing value of mem_tac
    apply GradedMonoid.toGradedMul.mul_mem (i := j ‚Ä¢ i) (j := (m - j) ‚Ä¢ i) <;> mem_tac_aux
    rw [‚Üê add_smul, Nat.add_sub_of_le h1]; rfl
  ¬∑ rw [(_ : m * i = _)]
    apply GradedMonoid.toGradedMul.mul_mem (i := (j-m) ‚Ä¢ i) (j := (m + m - j) ‚Ä¢ i) <;> mem_tac_aux
    rw [‚Üê add_smul]; congr; zify [le_of_not_lt h2, le_of_not_le h1]; abel
  convert_to ‚àë i in range (m + m + 1), g i ‚àà q.1; swap
  ¬∑ refine q.1.sum_mem fun j _ => nsmul_mem ?_ _; split_ifs
    exacts [q.1.zero_mem, q.1.mul_mem_left _ (hb i), q.1.mul_mem_right _ (ha i)]
  rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'']
  change _ = (algebraMap (HomogeneousLocalization.Away ùíú f) (Localization.Away f)) _
  dsimp only [Subtype.coe_mk]; rw [map_sum, mk_sum]
  apply Finset.sum_congr rfl fun j hj => _
  intro j hj
  change _ = HomogeneousLocalization.val _
  rw [HomogeneousLocalization.smul_val]
  split_ifs with h2 h1
  ¬∑ exact ((Finset.mem_range.1 hj).not_le h2).elim
  all_goals simp only [HomogeneousLocalization.mul_val, HomogeneousLocalization.zero_val,
    HomogeneousLocalization.val_mk'', Subtype.coe_mk, mk_mul, ‚Üê smul_mk]; congr 2
  ¬∑ dsimp; rw [mul_assoc, ‚Üê pow_add, add_comm (m - j), Nat.add_sub_assoc h1]
  ¬∑ simp_rw [pow_add]; rfl
  ¬∑ dsimp; rw [‚Üê mul_assoc, ‚Üê pow_add, Nat.add_sub_of_le (le_of_not_le h1)]
  ¬∑ simp_rw [pow_add]; rfl
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.add_mem AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.add_mem

variable (hm : 0 < m) (q : Spec.T A‚Å∞_ f)

theorem carrier.zero_mem : (0 : A) ‚àà carrier f_deg q := fun i => by
  convert Submodule.zero_mem q.1 using 1
  rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
    HomogeneousLocalization.zero_val]; simp_rw [map_zero, zero_pow hm.ne']
  convert Localization.mk_zero (S := Submonoid.powers f) _ using 1
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.zero_mem AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.zero_mem

theorem carrier.smul_mem (c x : A) (hx : x ‚àà carrier f_deg q) : c ‚Ä¢ x ‚àà carrier f_deg q := by
  revert c
  refine DirectSum.Decomposition.inductionOn ùíú ?_ ?_ ?_
  ¬∑ rw [zero_smul]; exact carrier.zero_mem f_deg hm _
  ¬∑ rintro n ‚ü®a, ha‚ü© i
    simp_rw [proj_apply, smul_eq_mul, coe_decompose_mul_of_left_mem ùíú i ha]
    -- Porting note: having trouble with Mul instance
    let product : A‚Å∞_ f :=
      Mul.mul (Quotient.mk'' ‚ü®_, ‚ü®a ^ m, pow_mem_graded m ha‚ü©, ‚ü®_, ?_‚ü©, ‚ü®n, rfl‚ü©‚ü© : A‚Å∞_ f)
          (Quotient.mk'' ‚ü®_, ‚ü®proj ùíú (i - n) x ^ m, by mem_tac‚ü©, ‚ü®_, ?_‚ü©, ‚ü®i - n, rfl‚ü©‚ü© : A‚Å∞_ f)
    ¬∑ split_ifs with h
      ¬∑ convert_to product ‚àà q.1
        ¬∑ dsimp [product]
          erw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
            HomogeneousLocalization.mul_val, HomogeneousLocalization.val_mk'',
            HomogeneousLocalization.val_mk'']
          ¬∑ simp_rw [mul_pow]; rw [Localization.mk_mul]
            ¬∑ congr; erw [‚Üê pow_add, Nat.add_sub_of_le h]
            ¬∑ rw [(_ : m ‚Ä¢ n = _)]
              ¬∑ mem_tac
              ¬∑ simp only [smul_eq_mul, mul_comm]
            ¬∑ rw [(_ : m ‚Ä¢ (i - n) = _)]
              ¬∑ mem_tac
              ¬∑ simp only [smul_eq_mul, mul_comm]
        ¬∑ apply Ideal.mul_mem_left (Œ± := A‚Å∞_ f) _ _ (hx _)
      ¬∑ simpa only [map_zero, zero_pow hm.ne'] using zero_mem f_deg hm q i
  ¬∑ simp_rw [add_smul]; exact fun _ _ => carrier.add_mem f_deg q
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.smul_mem AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.smul_mem

/-- For a prime ideal `q` in `A‚Å∞_f`, the set `{a | a·µ¢·µê/f‚Å± ‚àà q}` as an ideal.
-/
def carrier.asIdeal : Ideal A where
  carrier := carrier f_deg q
  zero_mem' := carrier.zero_mem f_deg hm q
  add_mem' := carrier.add_mem f_deg q
  smul_mem' := carrier.smul_mem f_deg hm q
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.as_ideal AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal


theorem carrier.asIdeal.homogeneous : (carrier.asIdeal f_deg hm q).IsHomogeneous ùíú :=
  fun i a ha j =>
  (em (i = j)).elim (fun h => h ‚ñ∏ by simpa only [proj_apply, decompose_coe, of_eq_same] using ha _)
    fun h => by
    simpa only [proj_apply, decompose_of_mem_ne ùíú (Submodule.coe_mem (decompose ùíú a i)) h,
      zero_pow hm.ne', map_zero] using carrier.zero_mem f_deg hm q j
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.as_ideal.homogeneous AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal.homogeneous

/-- For a prime ideal `q` in `A‚Å∞_f`, the set `{a | a·µ¢·µê/f‚Å± ‚àà q}` as a homogeneous ideal.
-/
def carrier.asHomogeneousIdeal : HomogeneousIdeal ùíú :=
  ‚ü®carrier.asIdeal f_deg hm q, carrier.asIdeal.homogeneous f_deg hm q‚ü©
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.as_homogeneous_ideal AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.asHomogeneousIdeal

theorem carrier.denom_not_mem : f ‚àâ carrier.asIdeal f_deg hm q := fun rid =>
  q.IsPrime.ne_top <|
    (Ideal.eq_top_iff_one _).mpr
      (by
        convert rid m
        rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.one_val,
          HomogeneousLocalization.val_mk'']
        dsimp
        simp_rw [decompose_of_mem_same _ f_deg]
        simp only [mk_eq_monoidOf_mk', Submonoid.LocalizationMap.mk'_self])
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.denom_not_mem AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.denom_not_mem

theorem carrier.relevant : ¬¨HomogeneousIdeal.irrelevant ùíú ‚â§ carrier.asHomogeneousIdeal f_deg hm q :=
  fun rid => carrier.denom_not_mem f_deg hm q <| rid <| DirectSum.decompose_of_mem_ne ùíú f_deg hm.ne'
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.relevant AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.relevant

theorem carrier.asIdeal.ne_top : carrier.asIdeal f_deg hm q ‚â† ‚ä§ := fun rid =>
  carrier.denom_not_mem f_deg hm q (rid.symm ‚ñ∏ Submodule.mem_top)
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.as_ideal.ne_top AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal.ne_top

theorem carrier.asIdeal.prime : (carrier.asIdeal f_deg hm q).IsPrime :=
  (carrier.asIdeal.homogeneous f_deg hm q).isPrime_of_homogeneous_mem_or_mem
    (carrier.asIdeal.ne_top f_deg hm q) fun {x y} ‚ü®nx, hnx‚ü© ‚ü®ny, hny‚ü© hxy =>
    show (‚àÄ i, _ ‚àà _) ‚à® ‚àÄ i, _ ‚àà _ by
      rw [‚Üê and_forall_ne nx, and_iff_left, ‚Üê and_forall_ne ny, and_iff_left]
      ¬∑ apply q.2.mem_or_mem; convert hxy (nx + ny) using 1
        dsimp
        simp_rw [decompose_of_mem_same ùíú hnx, decompose_of_mem_same ùíú hny,
          decompose_of_mem_same ùíú (SetLike.GradedMonoid.toGradedMul.mul_mem hnx hny),
          mul_pow, pow_add]
        simp only [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
          HomogeneousLocalization.mul_val, mk_mul]
        simp only [Submonoid.mk_mul_mk, mk_eq_monoidOf_mk']
      all_goals
        intro n hn; convert q.1.zero_mem using 1
        rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
          HomogeneousLocalization.zero_val]; simp_rw [proj_apply]
        convert mk_zero (S := Submonoid.powers f) _
        rw [decompose_of_mem_ne ùíú _ hn.symm, zero_pow hm.ne']
        ¬∑ first | exact hnx | exact hny
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.as_ideal.prime AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal.prime

/-- The function `Spec A‚Å∞_f ‚Üí Proj|D(f)` sending `q` to `{a | a·µ¢·µê/f‚Å± ‚àà q}`. -/
def toFun : (Spec.T A‚Å∞_ f) ‚Üí Proj.T| pbo f := fun q =>
  ‚ü®‚ü®carrier.asHomogeneousIdeal f_deg hm q, carrier.asIdeal.prime f_deg hm q,
      carrier.relevant f_deg hm q‚ü©,
    (ProjectiveSpectrum.mem_basicOpen _ f _).mp <| carrier.denom_not_mem f_deg hm q‚ü©
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.to_fun AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.toFun

end FromSpec

section toSpecFromSpec

lemma toSpec_fromSpec {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m) (hm : 0 < m) (x : Spec.T (A‚Å∞_ f)) :
    toSpec ùíú f (FromSpec.toFun f_deg hm x) = x := by
  apply PrimeSpectrum.ext
  apply le_antisymm <;> intros z hz
  ¬∑ obtain ‚ü®n, y, hy, hy', e‚ü© := (ToSpec.MemCarrier.eventually_exists f_deg _ hz).exists
    convert (FromSpec.mem_carrier_iff_of_mem_mul f_deg hm _ _ hy').mp hy
    apply HomogeneousLocalization.val_injective
    simp only [HomogeneousLocalization.val_mk'', mk_eq_mk', IsLocalization.eq_mk'_iff_mul_eq,
      map_pow, ‚Üê e, Algebra.smul_def, mul_comm z.val]
  ¬∑ refine (ToSpec.mem_carrier_iff_exists _ _).mpr ?_
    obtain ‚ü®n, y, hy, hn‚ü© := (HomogeneousLocalization.Away.eventually_smul_mem f_deg z).exists
    refine ‚ü®_, (FromSpec.mem_carrier_iff_of_mem_mul f_deg hm _ _ hy).mpr ?_, _, hn.symm‚ü©
    convert hz
    apply HomogeneousLocalization.val_injective
    rw [HomogeneousLocalization.val_mk'', Localization.mk_eq_mk',
      IsLocalization.mk'_eq_iff_eq_mul, hn, mul_comm, Algebra.smul_def]

@[deprecated (since := "2024-03-02")] alias toSpecFromSpec := toSpec_fromSpec

end toSpecFromSpec

section fromSpecToSpec

lemma fromSpec_toSpec {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m) (hm : 0 < m) (x : Proj.T| pbo f) :
    FromSpec.toFun f_deg hm (toSpec ùíú f x) = x := by
  refine Subtype.ext <| ProjectiveSpectrum.ext _ _ <| HomogeneousIdeal.ext' ?_
  intros i z hzi
  refine (FromSpec.mem_carrier_iff_of_mem f_deg hm _ _ hzi).trans ?_
  refine (ToSpec.mem_carrier_iff_exists _ _).trans ?_
  refine Iff.trans ?_ (x.1.2.pow_mem_iff_mem m hm)
  simp only [HomogeneousLocalization.val_mk'', Localization.mk_eq_mk', IsLocalization.smul_mk',
    IsLocalization.mk'_eq_iff_eq_mul, ‚Üê _root_.map_mul]
  constructor
  ¬∑ rintro ‚ü®y, hy, n, e‚ü©
    obtain ‚ü®‚ü®_, k, rfl‚ü©, e‚ü© := IsLocalization.exists_of_eq (M := Submonoid.powers f) e
    refine (x.1.2.mem_or_mem ?_).resolve_left (not_imp_not.mpr (x.1.2.mem_of_pow_mem (k + n)) x.2)
    simp only [‚Üê mul_assoc, ‚Üê pow_add] at e
    rw [e]
    exact (Ideal.mul_mem_right _ _ (Ideal.mul_mem_left _ _ hy))
  ¬∑ intro h
    exact ‚ü®_, h, i, by rw [mul_comm]‚ü©

lemma toSpec_injective {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m) (hm : 0 < m) :
    Function.Injective (toSpec ùíú f) := by
  intro x‚ÇÅ x‚ÇÇ h
  have := congr_arg (FromSpec.toFun f_deg hm) h
  rwa [fromSpec_toSpec, fromSpec_toSpec] at this

lemma toSpec_surjective {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m) (hm : 0 < m) :
    Function.Surjective (toSpec ùíú f) :=
  Function.surjective_iff_hasRightInverse |>.mpr
    ‚ü®FromSpec.toFun f_deg hm, toSpec_fromSpec ùíú f_deg hm‚ü©

lemma toSpec_bijective {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m) (hm : 0 < m):
    Function.Bijective (toSpec (ùíú := ùíú) (f := f)) :=
  ‚ü®toSpec_injective ùíú f_deg hm, toSpec_surjective ùíú f_deg hm‚ü©

end fromSpecToSpec

namespace toSpec

variable {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m) (hm : 0 < m)

variable {ùíú} in
lemma image_basicOpen_eq_basicOpen (a : A) (i : ‚Ñï) :
    toSpec ùíú f '' (Subtype.val ‚Åª¬π' (pbo (decompose ùíú a i) : Set (ProjectiveSpectrum ùíú))) =
    (PrimeSpectrum.basicOpen (R := A‚Å∞_ f) <|
      Quotient.mk'' ‚ü®m * i, ‚ü®decompose ùíú a i ^ m, SetLike.pow_mem_graded _ (Submodule.coe_mem _)‚ü©,
        ‚ü®f^i, by rw [mul_comm]; exact SetLike.pow_mem_graded _ f_deg‚ü©, ‚ü®i, rfl‚ü©‚ü©).1 :=
  Set.preimage_injective.mpr (toSpec_surjective ùíú f_deg hm) <|
    Set.preimage_image_eq _ (toSpec_injective ùíú f_deg hm) ‚ñ∏ by
  rw [Opens.carrier_eq_coe, toSpec_preimage_basicOpen, ProjectiveSpectrum.basicOpen_pow ùíú _ m hm]

end toSpec

variable {ùíú} in
/-- The continuous function `Spec A‚Å∞_f ‚Üí Proj|D(f)` sending `q` to `{a | a·µ¢·µê/f‚Å± ‚àà q}` where
`m` is the degree of `f` -/
def fromSpec {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m) (hm : 0 < m) :
    (Spec.T (A‚Å∞_ f)) ‚ü∂ (Proj.T| (pbo f)) where
  toFun := FromSpec.toFun f_deg hm
  continuous_toFun := by
    rw [isTopologicalBasis_subtype (ProjectiveSpectrum.isTopologicalBasis_basic_opens ùíú) (pbo f).1
      |>.continuous_iff]
    rintro s ‚ü®_, ‚ü®a, rfl‚ü©, rfl‚ü©
    have h‚ÇÅ : Subtype.val (p := (pbo f).1) ‚Åª¬π' (pbo a) =
        ‚ãÉ i : ‚Ñï, Subtype.val (p := (pbo f).1) ‚Åª¬π' (pbo (decompose ùíú a i)) := by
      simp [ProjectiveSpectrum.basicOpen_eq_union_of_projection ùíú a]
    let e : _ ‚âÉ _ :=
      ‚ü®FromSpec.toFun f_deg hm, ToSpec.toFun f, toSpec_fromSpec _ _ _, fromSpec_toSpec _ _ _‚ü©
    change IsOpen <| e ‚Åª¬π' _
    rw [Set.preimage_equiv_eq_image_symm, h‚ÇÅ, Set.image_iUnion]
    exact isOpen_iUnion fun i ‚Ü¶ toSpec.image_basicOpen_eq_basicOpen f_deg hm a i ‚ñ∏
      PrimeSpectrum.isOpen_basicOpen

end ProjIsoSpecTopComponent

variable {ùíú} in
/--
The homeomorphism `Proj|D(f) ‚âÖ Spec A‚Å∞_f` defined by
- `œÜ : Proj|D(f) ‚ü∂ Spec A‚Å∞_f` by sending `x` to `A‚Å∞_f ‚à© span {g / 1 | g ‚àà x}`
- `œà : Spec A‚Å∞_f ‚ü∂ Proj|D(f)` by sending `q` to `{a | a·µ¢·µê/f‚Å± ‚àà q}`.
-/
def projIsoSpecTopComponent {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m) (hm : 0 < m) :
    (Proj.T| (pbo f)) ‚âÖ (Spec.T (A‚Å∞_ f))  where
  hom := ProjIsoSpecTopComponent.toSpec ùíú f
  inv := ProjIsoSpecTopComponent.fromSpec f_deg hm
  hom_inv_id := ConcreteCategory.hom_ext _ _
    (ProjIsoSpecTopComponent.fromSpec_toSpec ùíú f_deg hm)
  inv_hom_id := ConcreteCategory.hom_ext _ _
    (ProjIsoSpecTopComponent.toSpec_fromSpec ùíú f_deg hm)

end AlgebraicGeometry
