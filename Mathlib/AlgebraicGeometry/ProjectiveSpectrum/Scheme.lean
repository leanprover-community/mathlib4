/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import Mathlib.AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf
import Mathlib.AlgebraicGeometry.Spec
import Mathlib.RingTheory.GradedAlgebra.Radical

#align_import algebraic_geometry.projective_spectrum.scheme from "leanprover-community/mathlib"@"d39590fc8728fbf6743249802486f8c91ffe07bc"

/-!
# Proj as a scheme

This file is to prove that `Proj` is a scheme.

## Notation

* `Proj`      : `Proj` as a locally ringed space
* `Proj.T`    : the underlying topological space of `Proj`
* `Proj| U`   : `Proj` restricted to some open set `U`
* `Proj.T| U` : the underlying topological space of `Proj` restricted to open set `U`
* `pbo f`     : basic open set at `f` in `Proj`
* `Spec`      : `Spec` as a locally ringed space
* `Spec.T`    : the underlying topological space of `Spec`
* `sbo g`     : basic open set at `g` in `Spec`
* `Aâ°_x`      : the degree zero part of localized ring `Aâ‚“`

## Implementation

In `AlgebraicGeometry/ProjectiveSpectrum/StructureSheaf.lean`, we have given `Proj` a
structure sheaf so that `Proj` is a locally ringed space. In this file we will prove that `Proj`
equipped with this structure sheaf is a scheme. We achieve this by using an affine cover by basic
open sets in `Proj`, more specifically:

1. We prove that `Proj` can be covered by basic open sets at homogeneous element of positive degree.
2. We prove that for any homogeneous element `f : A` of positive degree `m`, `Proj.T | (pbo f)` is
    homeomorphic to `Spec.T Aâ°_f`:
  - forward direction `toSpec`:
    for any `x : pbo f`, i.e. a relevant homogeneous prime ideal `x`, send it to
    `Aâ°_f âˆ© span {g / 1 | g âˆˆ x}` (see `ProjIsoSpecTopComponent.IoSpec.carrier`). This ideal is
    prime, the proof is in `ProjIsoSpecTopComponent.ToSpec.toFun`. The fact that this function
    is continuous is found in `ProjIsoSpecTopComponent.toSpec`
  - backward direction `fromSpec`:
    for any `q : Spec Aâ°_f`, we send it to `{a | âˆ€ i, aáµ¢áµ/fâ± âˆˆ q}`; we need this to be a
    homogeneous prime ideal that is relevant.
    * This is in fact an ideal, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal`;
    * This ideal is also homogeneous, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal.homogeneous`;
    * This ideal is relevant, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.relevant`;
    * This ideal is prime, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.prime`.
    Hence we have a well defined function `Spec.T Aâ°_f â†’ Proj.T | (pbo f)`, this function is called
    `ProjIsoSpecTopComponent.FromSpec.toFun`. But to prove the continuity of this function, we need
    to prove `fromSpec âˆ˜ toSpec` and `toSpec âˆ˜ fromSpec` are both identities; these are achieved in
    `ProjIsoSpecTopComponent.fromSpec_toSpec` and `ProjIsoSpecTopComponent.toSpec_fromSpec`.

## Main Definitions and Statements

For a homogeneous element `f` of degree `m`
* `ProjIsoSpecTopComponent.toSpec`: the continuous map between `Proj.T| pbo f` and `Spec.T Aâ°_f`
  defined by sending `x : Proj| (pbo f)` to `Aâ°_f âˆ© span {g / 1 | g âˆˆ x}`. We also denote this map
  as `Ïˆ`.
* `ProjIsoSpecTopComponent.ToSpec.preimage_eq`: for any `a: A`, if `a/f^m` has degree zero,
  then the preimage of `sbo a/f^m` under `to_Spec f` is `pbo f âˆ© pbo a`.

If we further assume `m` is positive
* `ProjIsoSpecTopComponent.fromSpec`: the continuous map between `Spec.T Aâ°_f` and `Proj.T| pbo f`
  defined by sending `q` to `{a | aáµ¢áµ/fâ± âˆˆ q}` where `aáµ¢` is the `i`-th coordinate of `a`.
  We also denote this map as `Ï†`
* `projIsoSpecTopComponent`: the homeomorphism `Proj.T| pbo f â‰… Spec.T Aâ°_f` obtained by `Ï†` and
  `Ïˆ`.
## Reference
* [Robin Hartshorne, *Algebraic Geometry*][Har77]: Chapter II.2 Proposition 2.5
-/


noncomputable section

set_option linter.uppercaseLean3 false -- Porting note: Proj and Spec

namespace AlgebraicGeometry

open scoped DirectSum BigOperators Pointwise BigOperators

open DirectSum SetLike.GradedMonoid Localization

open Finset hiding mk_zero

variable {R A : Type*}
variable [CommRing R] [CommRing A] [Algebra R A]
variable (ğ’œ : â„• â†’ Submodule R A)
variable [GradedAlgebra ğ’œ]

open TopCat TopologicalSpace

open CategoryTheory Opposite

open ProjectiveSpectrum.StructureSheaf

-- Porting note: currently require lack of hygiene to use in variable declarations
-- maybe all make into notation3?
set_option hygiene false
/-- `Proj` as a locally ringed space -/
local notation3 "Proj" => Proj.toLocallyRingedSpace ğ’œ

/-- The underlying topological space of `Proj` -/
local notation3 "Proj.T" => PresheafedSpace.carrier <| SheafedSpace.toPresheafedSpace
  <| LocallyRingedSpace.toSheafedSpace <| Proj.toLocallyRingedSpace ğ’œ

/-- `Proj` restrict to some open set -/
macro "Proj| " U:term : term =>
  `((Proj.toLocallyRingedSpace ğ’œ).restrict (Opens.openEmbedding (X := Proj.T) ($U : Opens Proj.T)))

/-- the underlying topological space of `Proj` restricted to some open set -/
local notation "Proj.T| " U => PresheafedSpace.carrier <| SheafedSpace.toPresheafedSpace
  <| LocallyRingedSpace.toSheafedSpace
    <| (LocallyRingedSpace.restrict Proj (Opens.openEmbedding (X := Proj.T) (U : Opens Proj.T)))

/-- basic open sets in `Proj` -/
local notation "pbo " x => ProjectiveSpectrum.basicOpen ğ’œ x

/-- basic open sets in `Spec` -/
local notation "sbo " f => PrimeSpectrum.basicOpen f

/-- `Spec` as a locally ringed space -/
local notation3 "Spec " ring => Spec.locallyRingedSpaceObj (CommRingCat.of ring)

/-- the underlying topological space of `Spec` -/
local notation "Spec.T " ring =>
  (Spec.locallyRingedSpaceObj (CommRingCat.of ring)).toSheafedSpace.toPresheafedSpace.1

local notation3 "Aâ°_ " f => HomogeneousLocalization.Away ğ’œ f

namespace ProjIsoSpecTopComponent

/-
This section is to construct the homeomorphism between `Proj` restricted at basic open set at
a homogeneous element `x` and `Spec Aâ°â‚“` where `Aâ°â‚“` is the degree zero part of the localized
ring `Aâ‚“`.
-/
namespace ToSpec

open Ideal

-- This section is to construct the forward direction :
-- So for any `x` in `Proj| (pbo f)`, we need some point in `Spec Aâ°_f`, i.e. a prime ideal,
-- and we need this correspondence to be continuous in their Zariski topology.
variable {ğ’œ} {f : A} {m : â„•} (f_deg : f âˆˆ ğ’œ m) (x : Proj| (pbo f))

/--
For any `x` in `Proj| (pbo f)`, the corresponding ideal in `Spec Aâ°_f`. This fact that this ideal
is prime is proven in `TopComponent.Forward.toFun`-/
def carrier : Ideal (Aâ°_ f) :=
  Ideal.comap (algebraMap (Aâ°_ f) (Away f))
    (Ideal.span <| algebraMap A (Away f) '' x.val.asHomogeneousIdeal)
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec.carrier AlgebraicGeometry.ProjIsoSpecTopComponent.ToSpec.carrier

theorem mem_carrier_iff (z : Aâ°_ f) :
    z âˆˆ carrier x â†” z.val âˆˆ Ideal.span (algebraMap A (Away f) '' x.1.asHomogeneousIdeal) :=
  Iff.rfl
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec.mem_carrier_iff AlgebraicGeometry.ProjIsoSpecTopComponent.ToSpec.mem_carrier_iff

theorem MemCarrier.clear_denominator' [DecidableEq (Away f)] {z : Localization.Away f}
    (hz : z âˆˆ span (algebraMap A (Away f) '' x.val.asHomogeneousIdeal)) :
    âˆƒ (c : algebraMap A (Away f) '' x.1.asHomogeneousIdeal â†’â‚€ Away f) (N : â„•) (acd :
      âˆ€ y âˆˆ c.support.image c, A),
      f ^ N â€¢ z =
        algebraMap A (Away f)
          (âˆ‘ i in c.support.attach,
            acd (c i) (Finset.mem_image.mpr âŸ¨i, âŸ¨i.2, rflâŸ©âŸ©) * i.1.2.choose) := by
  rw [â† submodule_span_eq, Finsupp.span_eq_range_total, LinearMap.mem_range] at hz
  rcases hz with âŸ¨c, eq1âŸ©
  rw [Finsupp.total_apply, Finsupp.sum] at eq1
  obtain âŸ¨âŸ¨_, N, rflâŸ©, hNâŸ© :=
    IsLocalization.exist_integer_multiples_of_finset (Submonoid.powers f) (c.support.image c)
  choose acd hacd using hN
  refine' âŸ¨c, N, acd, _âŸ©
  rw [â† eq1, smul_sum, map_sum, â† sum_attach]
  congr 1
  ext i
  rw [_root_.map_mul, hacd, (Classical.choose_spec i.1.2).2, smul_eq_mul, smul_mul_assoc]
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec.mem_carrier.clear_denominator' AlgebraicGeometry.ProjIsoSpecTopComponent.ToSpec.MemCarrier.clear_denominator'

theorem MemCarrier.clear_denominator [DecidableEq (Away f)] {z : Aâ°_ f} (hz : z âˆˆ carrier x) :
    âˆƒ (c : algebraMap A (Away f) '' x.1.asHomogeneousIdeal â†’â‚€ Away f) (N : â„•) (acd :
      âˆ€ y âˆˆ c.support.image c, A),
      f ^ N â€¢ z.val =
        algebraMap A (Away f)
          (âˆ‘ i in c.support.attach,
            acd (c i) (Finset.mem_image.mpr âŸ¨i, âŸ¨i.2, rflâŸ©âŸ©) * i.1.2.choose) :=
  MemCarrier.clear_denominator' x <| (mem_carrier_iff x z).mpr hz
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec.mem_carrier.clear_denominator AlgebraicGeometry.ProjIsoSpecTopComponent.ToSpec.MemCarrier.clear_denominator

/-- For `x : pbo f`, the ideal `Aâ°_f âˆ© span {g / 1 | g âˆˆ x}`
is equal to `span {a/f^n | a âˆˆ x and deg(a) = deg(f^n)}`. -/
lemma carrier_eq_span :
    carrier x =
    Ideal.span { z : HomogeneousLocalization.Away ğ’œ f |
      âˆƒ (s F : A) (_ : s âˆˆ x.1.asHomogeneousIdeal) (n : â„•)
        (s_mem : s âˆˆ ğ’œ n) (F_mem1 : F âˆˆ ğ’œ n) (F_mem2 : F âˆˆ Submonoid.powers f),
        z = Quotient.mk'' âŸ¨n, âŸ¨s, s_memâŸ©, âŸ¨F, F_mem1âŸ©,F_mem2âŸ© } := by
  classical
  refine le_antisymm ?_ <| Ideal.span_le.mpr ?_
  Â· intro z hz
    let k : â„• := z.den_mem.choose
    have hk : f^k = z.den := z.den_mem.choose_spec
    suffices mem1 : z.num âˆˆ x.1.asHomogeneousIdeal by
      refine Ideal.subset_span âŸ¨_, _, mem1, _, z.num_mem_deg, z.den_mem_deg, z.den_mem, ?_âŸ©
      rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
        HomogeneousLocalization.eq_num_div_den]

    obtain âŸ¨c, N, acd, eq1âŸ© := MemCarrier.clear_denominator x hz
    rw [z.eq_num_div_den, smul_mk, smul_eq_mul, â† mk_one_eq_algebraMap,
      Localization.mk_eq_mk_iff, Localization.r_iff_exists] at eq1
    obtain âŸ¨âŸ¨_, âŸ¨l, rflâŸ©âŸ©, eq1âŸ© := eq1
    dsimp only [OneMemClass.coe_one] at eq1
    rw [one_mul, â† hk, â† mul_assoc, â† mul_assoc, â† pow_add, â† pow_add] at eq1

    suffices f^(l + k) * âˆ‘ i in c.support.attach, acd (c i) _ * i.1.2.choose âˆˆ
      x.1.asHomogeneousIdeal from
      (x.1.isPrime.mem_or_mem (eq1.symm â–¸ this)).resolve_left fun r â†¦
        (ProjectiveSpectrum.mem_basicOpen ğ’œ _ _).mp x.2 <| x.1.isPrime.mem_of_pow_mem _ r
    exact Ideal.mul_mem_left _ _ <| Ideal.sum_mem _ (fun âŸ¨j, hjâŸ© _ â†¦
      Ideal.mul_mem_left _ _ j.2.choose_spec.1)

  Â· rintro _ âŸ¨s, _, hs, n, s_mem, F_mem, âŸ¨l, rflâŸ©, rflâŸ©
    erw [mem_carrier_iff, HomogeneousLocalization.val_mk'']
    erw [show (Localization.mk s âŸ¨f ^ l, âŸ¨_, rflâŸ©âŸ© : Localization.Away f) =
      (Localization.mk 1 âŸ¨f^l, âŸ¨_, rflâŸ©âŸ© : Localization.Away f) * Localization.mk s 1 by
      Â· rw [Localization.mk_mul, mul_one, one_mul]]
    exact Ideal.mul_mem_left _ _ <| Ideal.subset_span âŸ¨s, hs, rflâŸ©

theorem disjoint :
    Disjoint (x.1.asHomogeneousIdeal.toIdeal : Set A) (Submonoid.powers f : Set A) := by
  by_contra rid
  rw [Set.not_disjoint_iff] at rid
  choose g hg using rid
  obtain âŸ¨hg1, âŸ¨k, rflâŸ©âŸ© := hg
  by_cases k_ineq : 0 < k
  Â· erw [x.1.isPrime.pow_mem_iff_mem _ k_ineq] at hg1
    exact x.2 hg1
  Â· dsimp at hg1
    erw [show k = 0 by omega, pow_zero, â† Ideal.eq_top_iff_one] at hg1
    apply x.1.isPrime.1
    exact hg1
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec.disjoint AlgebraicGeometry.ProjIsoSpecTopComponent.ToSpec.disjoint

theorem carrier_ne_top : carrier x â‰  âŠ¤ := by
  have eq_top := disjoint x
  classical
  contrapose! eq_top
  obtain âŸ¨c, N, acd, eq1âŸ© := MemCarrier.clear_denominator x ((Ideal.eq_top_iff_one _).mp eq_top)
  rw [Algebra.smul_def, HomogeneousLocalization.one_val, mul_one] at eq1
  change Localization.mk (f ^ N) 1 = Localization.mk _ 1 at eq1
  simp only [mk_eq_mk', IsLocalization.eq] at eq1
  rcases eq1 with âŸ¨âŸ¨_, âŸ¨M, rflâŸ©âŸ©, eq1âŸ©
  dsimp at eq1
  erw [one_mul, one_mul] at eq1
  change f ^ _ * f ^ _ = f ^ _ * _ at eq1
  rw [Set.not_disjoint_iff_nonempty_inter]
  refine'
    âŸ¨f ^ M * f ^ N, eq1.symm â–¸ mul_mem_left _ _ (sum_mem _ fun i _ => mul_mem_left _ _ _),
      âŸ¨M + N, by dsimp; rw [pow_add]âŸ©âŸ©
  generalize_proofs hâ‚ hâ‚‚
  exact (Classical.choose_spec hâ‚‚).1
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec.carrier_ne_top AlgebraicGeometry.ProjIsoSpecTopComponent.ToSpec.carrier_ne_top

variable (f)

/-- The function between the basic open set `D(f)` in `Proj` to the corresponding basic open set in
`Spec Aâ°_f`. This is bundled into a continuous map in `TopComponent.forward`.
-/
def toFun (x : Proj.T| pbo f) : Spec.T Aâ°_ f :=
  âŸ¨carrier x, carrier_ne_top x, fun {x1 x2} hx12 => by
    classical
    rw [mem_carrier_iff] at hx12
    let J := span ((algebraMap A (Away f) : A â†’ (Away f)) '' x.val.asHomogeneousIdeal)
    suffices h : âˆ€ x y : Localization.Away f, x * y âˆˆ J â†’ x âˆˆ J âˆ¨ y âˆˆ J by
      rw [HomogeneousLocalization.mul_val] at hx12; exact h x1.val x2.val hx12
    clear x1 x2 hx12
    intro x1 x2 hx12
    induction' x1 using Localization.induction_on with data_x1
    induction' x2 using Localization.induction_on with data_x2
    rcases data_x1, data_x2 with âŸ¨âŸ¨a1, _, âŸ¨n1, rflâŸ©âŸ©, âŸ¨a2, _, âŸ¨n2, rflâŸ©âŸ©âŸ©
    rcases MemCarrier.clear_denominator' x hx12 with âŸ¨c, N, acd, eq1âŸ©
    simp only [Algebra.smul_def] at eq1
    change Localization.mk (f ^ N) 1 * (Localization.mk _ _ * Localization.mk _ _)
      = Localization.mk _ _ at eq1
    simp only [Localization.mk_mul, one_mul] at eq1
    simp only [mk_eq_mk', IsLocalization.eq] at eq1
    rcases eq1 with âŸ¨âŸ¨_, âŸ¨M, rflâŸ©âŸ©, eq1âŸ©
    rw [Submonoid.coe_one, one_mul] at eq1
    change f ^ _ * (_ * _) = f ^ _ * (f ^ _ * f ^ _ * _) at eq1
    have that : a1 * a2 * f ^ N * f ^ M âˆˆ x.val.asHomogeneousIdeal.toIdeal := ?_
    rcases x.1.isPrime.mem_or_mem (show a1 * a2 * f ^ N * f ^ M âˆˆ _ from that) with (h1 | rid2)
    rcases x.1.isPrime.mem_or_mem h1 with (h1 | rid1)
    rcases x.1.isPrime.mem_or_mem h1 with (h1 | h2)
    Â· left;
      simp only [show (Localization.mk a1 âŸ¨f ^ n1, _âŸ© : Away f) =
        Localization.mk a1 1 * Localization.mk 1 (âŸ¨f ^ n1, âŸ¨n1, rflâŸ©âŸ© : Submonoid.powers f) by
          rw [Localization.mk_mul, mul_one, one_mul]]
      exact Ideal.mul_mem_right _ _ (Ideal.subset_span âŸ¨_, h1, rflâŸ©)
    Â· right;
      simp only [show (mk a2 âŸ¨f ^ n2, _âŸ© : Away f) =
        mk a2 1 * Localization.mk 1 (âŸ¨f ^ n2, âŸ¨n2, rflâŸ©âŸ© : Submonoid.powers f) by
          rw [Localization.mk_mul, mul_one, one_mul]]
      exact Ideal.mul_mem_right _ _ (Ideal.subset_span âŸ¨_, h2, rflâŸ©)
    Â· exact False.elim (x.2 (x.1.isPrime.mem_of_pow_mem N rid1))
    Â· exact False.elim (x.2 (x.1.isPrime.mem_of_pow_mem M rid2))
    Â· rw [â† mul_comm (f ^ M), â† mul_comm (f ^ N), eq1]
      refine' mul_mem_left _ _ (mul_mem_left _ _ (sum_mem _ fun i _ => mul_mem_left _ _ _))
      generalize_proofs hâ‚ hâ‚‚; exact (Classical.choose_spec hâ‚‚).1âŸ©
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec.to_fun AlgebraicGeometry.ProjIsoSpecTopComponent.ToSpec.toFun

/-
The preimage of basic open set `D(a/f^n)` in `Spec Aâ°_f` under the forward map from `Proj A` to
`Spec Aâ°_f` is the basic open set `D(a) âˆ© D(f)` in `Proj A`. This lemma is used to prove that the
forward map is continuous.
-/
theorem preimage_eq (a b : A) (k : â„•) (a_mem : a âˆˆ ğ’œ k) (b_mem1 : b âˆˆ ğ’œ k)
    (b_mem2 : b âˆˆ Submonoid.powers f) :
    toFun f â»Â¹'
        (@PrimeSpectrum.basicOpen (Aâ°_ f) _ (Quotient.mk'' âŸ¨k, âŸ¨a, a_memâŸ©, âŸ¨b, b_mem1âŸ©, b_mem2âŸ©) :
          Set (PrimeSpectrum (HomogeneousLocalization.Away ğ’œ f))) =
      {x | x.1 âˆˆ (pbo f) âŠ“ pbo a} := by
  classical
  ext1 y
  constructor <;> intro hy
  Â· refine' âŸ¨y.2, _âŸ©
    rw [Set.mem_preimage, SetLike.mem_coe, PrimeSpectrum.mem_basicOpen] at hy
    rw [ProjectiveSpectrum.mem_coe_basicOpen]
    intro a_mem_y
    apply hy
    rw [toFun, mem_carrier_iff, HomogeneousLocalization.val_mk'', Subtype.coe_mk]
    dsimp; rcases b_mem2 with âŸ¨k, hkâŸ©
    dsimp at hk
    simp only [show (mk a âŸ¨b, âŸ¨k, hkâŸ©âŸ© : Away f) =
      Localization.mk 1 (âŸ¨f ^ k, âŸ¨_, rflâŸ©âŸ© : Submonoid.powers f) * mk a 1 by
        rw [mk_mul, one_mul, mul_one]; congr; rw [hk]]
    exact Ideal.mul_mem_left _ _ (Ideal.subset_span âŸ¨_, a_mem_y, rflâŸ©)
  Â· change y.1 âˆˆ ProjectiveSpectrum.basicOpen ğ’œ f âŠ“ ProjectiveSpectrum.basicOpen ğ’œ a at hy
    rcases hy with âŸ¨hy1, hy2âŸ©
    rw [ProjectiveSpectrum.mem_coe_basicOpen] at hy1 hy2
    rw [Set.mem_preimage, toFun, SetLike.mem_coe, PrimeSpectrum.mem_basicOpen]
    intro rid; dsimp at rid
    rcases MemCarrier.clear_denominator _ rid with âŸ¨c, N, acd, eq1âŸ©
    rw [Algebra.smul_def] at eq1
    change Localization.mk (f ^ N) 1 * Localization.mk _ _ = Localization.mk _ _ at eq1
    rw [mk_mul, one_mul, mk_eq_mk', IsLocalization.eq] at eq1
    rcases eq1 with âŸ¨âŸ¨_, âŸ¨M, rflâŸ©âŸ©, eq1âŸ©
    rw [Submonoid.coe_one, one_mul] at eq1
    simp only [Subtype.coe_mk] at eq1
    have : a * f ^ N * f ^ M âˆˆ y.val.asHomogeneousIdeal.toIdeal := by
      rw [mul_comm _ (f ^ N), mul_comm _ (f ^ M), eq1]
      refine' mul_mem_left _ _ (mul_mem_left _ _ (sum_mem _ fun i _ => mul_mem_left _ _ _))
      generalize_proofs hâ‚ hâ‚‚; exact (Classical.choose_spec hâ‚‚).1
    rcases y.1.isPrime.mem_or_mem this with (H1 | H3)
    rcases y.1.isPrime.mem_or_mem H1 with (H1 | H2)
    Â· exact hy2 H1
    Â· exact y.2 (y.1.isPrime.mem_of_pow_mem N H2)
    Â· exact y.2 (y.1.isPrime.mem_of_pow_mem M H3)
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec.preimage_eq AlgebraicGeometry.ProjIsoSpecTopComponent.ToSpec.preimage_eq

end ToSpec

section

/-- The continuous function from the basic open set `D(f)` in `Proj`
to the corresponding basic open set in `Spec Aâ°_f`. -/
def toSpec (f : A) : (Proj.T| pbo f) âŸ¶ Spec.T Aâ°_ f where
  toFun := ToSpec.toFun f
  continuous_toFun := by
    rw [PrimeSpectrum.isTopologicalBasis_basic_opens.continuous_iff]
    rintro _ âŸ¨âŸ¨k, âŸ¨a, haâŸ©, âŸ¨b, hb1âŸ©, âŸ¨k', hb2âŸ©âŸ©, rflâŸ©; dsimp
    erw [ToSpec.preimage_eq f a b k ha hb1 âŸ¨k', hb2âŸ©]
    refine' isOpen_induced_iff.mpr âŸ¨(pbo f).1 âŠ“ (pbo a).1, IsOpen.inter (pbo f).2 (pbo a).2, _âŸ©
    ext z; constructor
    Â· intro hz; simpa [Set.mem_preimage]
    Â· intro hz; simpa only [Set.inf_eq_inter,Set.mem_inter_iff,Set.mem_preimage]
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec AlgebraicGeometry.ProjIsoSpecTopComponent.toSpec

variable {ğ’œ} in
lemma toSpec_preimage_eq {f : A} (a b : A) (k : â„•) (a_mem : a âˆˆ ğ’œ k) (b_mem1 : b âˆˆ ğ’œ k)
    (b_mem2 : b âˆˆ Submonoid.powers f) :
    toSpec ğ’œ f â»Â¹'
        (@PrimeSpectrum.basicOpen (Aâ°_ f) _ (Quotient.mk'' âŸ¨k, âŸ¨a, a_memâŸ©, âŸ¨b, b_mem1âŸ©, b_mem2âŸ©) :
          Set (PrimeSpectrum (HomogeneousLocalization.Away ğ’œ f))) =
      {x | x.1 âˆˆ (pbo f) âŠ“ pbo a} :=
  ToSpec.preimage_eq f a b k a_mem b_mem1 b_mem2

end

namespace FromSpec

open GradedAlgebra SetLike

open Finset hiding mk_zero

-- Porting note: _root_ doesn't work here
open HomogeneousLocalization

variable {ğ’œ} {f : A} {m : â„•} (f_deg : f âˆˆ ğ’œ m)

open Lean Meta Elab Tactic

macro "mem_tac_aux" : tactic =>
  `(tactic| first | exact pow_mem_graded _ (Submodule.coe_mem _) | exact nat_cast_mem_graded _ _ |
    exact pow_mem_graded _ f_deg)

macro "mem_tac" : tactic =>
  `(tactic| first | mem_tac_aux |
    repeat (all_goals (apply SetLike.GradedMonoid.toGradedMul.mul_mem)); mem_tac_aux)

/-- The function from `Spec Aâ°_f` to `Proj|D(f)` is defined by `q â†¦ {a | aáµ¢áµ/fâ± âˆˆ q}`, i.e. sending
`q` a prime ideal in `Aâ°_f` to the homogeneous prime relevant ideal containing only and all the
elements `a : A` such that for every `i`, the degree 0 element formed by dividing the `m`-th power
of the `i`-th projection of `a` by the `i`-th power of the degree-`m` homogeneous element `f`,
lies in `q`.

The set `{a | aáµ¢áµ/fâ± âˆˆ q}`
* is an ideal, as proved in `carrier.asIdeal`;
* is homogeneous, as proved in `carrier.asHomogeneousIdeal`;
* is prime, as proved in `carrier.asIdeal.prime`;
* is relevant, as proved in `carrier.relevant`.
-/
def carrier (q : Spec.T Aâ°_ f) : Set A :=
  {a | âˆ€ i, (Quotient.mk'' âŸ¨m * i, âŸ¨proj ğ’œ i a ^ m, by mem_tacâŸ©,
              âŸ¨f ^ i, by rw [mul_comm]; mem_tacâŸ©, âŸ¨_, rflâŸ©âŸ© : Aâ°_ f) âˆˆ q.1}
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier

theorem mem_carrier_iff (q : Spec.T Aâ°_ f) (a : A) :
    a âˆˆ carrier f_deg q â†” âˆ€ i, (Quotient.mk'' âŸ¨m * i, âŸ¨proj ğ’œ i a ^ m, by mem_tacâŸ©,
      âŸ¨f ^ i, by rw [mul_comm]; mem_tacâŸ©, âŸ¨_, rflâŸ©âŸ© : Aâ°_ f) âˆˆ q.1 :=
  Iff.rfl
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.mem_carrier_iff AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.mem_carrier_iff

theorem mem_carrier_iff' (q : Spec.T Aâ°_ f) (a : A) :
    a âˆˆ carrier f_deg q â†”
      âˆ€ i, (Localization.mk (proj ğ’œ i a ^ m) âŸ¨f ^ i, âŸ¨i, rflâŸ©âŸ© : Localization.Away f) âˆˆ
          algebraMap (HomogeneousLocalization.Away ğ’œ f) (Localization.Away f) '' { s | s âˆˆ q.1 } :=
  (mem_carrier_iff f_deg q a).trans
    (by
      constructor <;> intro h i <;> specialize h i
      Â· rw [Set.mem_image]; refine' âŸ¨_, h, rflâŸ©
      Â· rw [Set.mem_image] at h; rcases h with âŸ¨x, h, hxâŸ©
        change x âˆˆ q.asIdeal at h
        convert h
        rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'']
        dsimp only [Subtype.coe_mk]; rw [â† hx]; rfl)
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.mem_carrier_iff' AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.mem_carrier_iff'

theorem carrier.add_mem (q : Spec.T Aâ°_ f) {a b : A} (ha : a âˆˆ carrier f_deg q)
    (hb : b âˆˆ carrier f_deg q) : a + b âˆˆ carrier f_deg q := by
  refine' fun i => (q.2.mem_or_mem _).elim id id
  change (Quotient.mk'' âŸ¨_, _, _, _âŸ© : Aâ°_ f) âˆˆ q.1; dsimp only [Subtype.coe_mk]
  simp_rw [â† pow_add, map_add, add_pow, mul_comm, â† nsmul_eq_mul]
  let g : â„• â†’ Aâ°_ f := fun j => (m + m).choose j â€¢
      if h2 : m + m < j then (0 : Aâ°_ f)
      else
        -- Porting note: inlining `l`, `r` causes a "can't synth HMul Aâ°_ f Aâ°_ f ?" error
        if h1 : j â‰¤ m then
          letI l : Aâ°_ f := Quotient.mk''
            âŸ¨m * i, âŸ¨proj ğ’œ i a ^ j * proj ğ’œ i b ^ (m - j), ?_âŸ©,
              âŸ¨_, by rw [mul_comm]; mem_tacâŸ©, âŸ¨i, rflâŸ©âŸ©
          letI r : Aâ°_ f := Quotient.mk''
            âŸ¨m * i, âŸ¨proj ğ’œ i b ^ m, by mem_tacâŸ©,
              âŸ¨_, by rw [mul_comm]; mem_tacâŸ©, âŸ¨i, rflâŸ©âŸ©
          l * r
        else
          letI l : Aâ°_ f := Quotient.mk''
            âŸ¨m * i, âŸ¨proj ğ’œ i a ^ m, by mem_tacâŸ©,
              âŸ¨_, by rw [mul_comm]; mem_tacâŸ©, âŸ¨i, rflâŸ©âŸ©
          letI r : Aâ°_ f := Quotient.mk''
            âŸ¨m * i, âŸ¨proj ğ’œ i a ^ (j - m) * proj ğ’œ i b ^ (m + m - j), ?_âŸ©,
              âŸ¨_, by rw [mul_comm]; mem_tacâŸ©, âŸ¨i, rflâŸ©âŸ©
          l * r
  rotate_left
  Â· rw [(_ : m * i = _)]
    -- Porting note: it seems unification with mul_mem is more fiddly reducing value of mem_tac
    apply GradedMonoid.toGradedMul.mul_mem (i := j â€¢ i) (j := (m - j) â€¢ i) <;> mem_tac_aux
    rw [â† add_smul, Nat.add_sub_of_le h1]; rfl
  Â· rw [(_ : m * i = _)]
    apply GradedMonoid.toGradedMul.mul_mem (i := (j-m) â€¢ i) (j := (m + m - j) â€¢ i) <;> mem_tac_aux
    rw [â† add_smul]; congr; zify [le_of_not_lt h2, le_of_not_le h1]; abel
  convert_to âˆ‘ i in range (m + m + 1), g i âˆˆ q.1; swap
  Â· refine' q.1.sum_mem fun j _ => nsmul_mem _ _; split_ifs
    exacts [q.1.zero_mem, q.1.mul_mem_left _ (hb i), q.1.mul_mem_right _ (ha i)]
  rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'']
  change _ = (algebraMap (HomogeneousLocalization.Away ğ’œ f) (Localization.Away f)) _
  dsimp only [Subtype.coe_mk]; rw [map_sum, mk_sum]
  apply Finset.sum_congr rfl fun j hj => _
  intro j hj
  change _ = HomogeneousLocalization.val _
  rw [HomogeneousLocalization.smul_val]
  split_ifs with h2 h1
  Â· exact ((Finset.mem_range.1 hj).not_le h2).elim
  all_goals simp only [HomogeneousLocalization.mul_val, HomogeneousLocalization.zero_val,
    HomogeneousLocalization.val_mk'', Subtype.coe_mk, mk_mul, â† smul_mk]; congr 2
  Â· dsimp; rw [mul_assoc, â† pow_add, add_comm (m - j), Nat.add_sub_assoc h1]
  Â· simp_rw [pow_add]; rfl
  Â· dsimp; rw [â† mul_assoc, â† pow_add, Nat.add_sub_of_le (le_of_not_le h1)]
  Â· simp_rw [pow_add]; rfl
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.add_mem AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.add_mem

variable (hm : 0 < m) (q : Spec.T Aâ°_ f)

theorem carrier.zero_mem : (0 : A) âˆˆ carrier f_deg q := fun i => by
  convert Submodule.zero_mem q.1 using 1
  rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
    HomogeneousLocalization.zero_val]; simp_rw [map_zero, zero_pow hm.ne']
  convert Localization.mk_zero (S := Submonoid.powers f) _ using 1
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.zero_mem AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.zero_mem

theorem carrier.smul_mem (c x : A) (hx : x âˆˆ carrier f_deg q) : c â€¢ x âˆˆ carrier f_deg q := by
  revert c
  refine' DirectSum.Decomposition.inductionOn ğ’œ _ _ _
  Â· rw [zero_smul]; exact carrier.zero_mem f_deg hm _
  Â· rintro n âŸ¨a, haâŸ© i
    simp_rw [proj_apply, smul_eq_mul, coe_decompose_mul_of_left_mem ğ’œ i ha]
    -- Porting note: having trouble with Mul instance
    let product : Aâ°_ f :=
      Mul.mul (Quotient.mk'' âŸ¨_, âŸ¨a ^ m, pow_mem_graded m haâŸ©, âŸ¨_, ?_âŸ©, âŸ¨n, rflâŸ©âŸ© : Aâ°_ f)
          (Quotient.mk'' âŸ¨_, âŸ¨proj ğ’œ (i - n) x ^ m, by mem_tacâŸ©, âŸ¨_, ?_âŸ©, âŸ¨i - n, rflâŸ©âŸ© : Aâ°_ f)
    split_ifs with h
    Â· convert_to product âˆˆ q.1
      Â· dsimp [product]
        erw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
          HomogeneousLocalization.mul_val, HomogeneousLocalization.val_mk'',
          HomogeneousLocalization.val_mk'']
        simp_rw [mul_pow]; rw [Localization.mk_mul]
        congr; erw [â† pow_add, Nat.add_sub_of_le h]
        Â· rw [(_ : m â€¢ n = _)]; mem_tac; simp only [smul_eq_mul, mul_comm]
        Â· rw [(_ : m â€¢ (i - n) = _)]; mem_tac; simp only [smul_eq_mul, mul_comm]
      Â· apply Ideal.mul_mem_left (Î± := Aâ°_ f) _ _ (hx _)
    Â· simpa only [map_zero, zero_pow hm.ne'] using zero_mem f_deg hm q i
  Â· simp_rw [add_smul]; exact fun _ _ => carrier.add_mem f_deg q
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.smul_mem AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.smul_mem

/-- For a prime ideal `q` in `Aâ°_f`, the set `{a | aáµ¢áµ/fâ± âˆˆ q}` as an ideal.
-/
def carrier.asIdeal : Ideal A where
  carrier := carrier f_deg q
  zero_mem' := carrier.zero_mem f_deg hm q
  add_mem' := carrier.add_mem f_deg q
  smul_mem' := carrier.smul_mem f_deg hm q
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.as_ideal AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal


theorem carrier.asIdeal.homogeneous : (carrier.asIdeal f_deg hm q).IsHomogeneous ğ’œ :=
  fun i a ha j =>
  (em (i = j)).elim (fun h => h â–¸ by simpa only [proj_apply, decompose_coe, of_eq_same] using ha _)
    fun h => by
    simpa only [proj_apply, decompose_of_mem_ne ğ’œ (Submodule.coe_mem (decompose ğ’œ a i)) h,
      zero_pow hm.ne', map_zero] using carrier.zero_mem f_deg hm q j
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.as_ideal.homogeneous AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal.homogeneous

/-- For a prime ideal `q` in `Aâ°_f`, the set `{a | aáµ¢áµ/fâ± âˆˆ q}` as a homogeneous ideal.
-/
def carrier.asHomogeneousIdeal : HomogeneousIdeal ğ’œ :=
  âŸ¨carrier.asIdeal f_deg hm q, carrier.asIdeal.homogeneous f_deg hm qâŸ©
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.as_homogeneous_ideal AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.asHomogeneousIdeal

theorem carrier.denom_not_mem : f âˆ‰ carrier.asIdeal f_deg hm q := fun rid =>
  q.IsPrime.ne_top <|
    (Ideal.eq_top_iff_one _).mpr
      (by
        convert rid m
        rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.one_val,
          HomogeneousLocalization.val_mk'']
        dsimp
        simp_rw [decompose_of_mem_same _ f_deg]
        simp only [mk_eq_monoidOf_mk', Submonoid.LocalizationMap.mk'_self])
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.denom_not_mem AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.denom_not_mem

theorem carrier.relevant : Â¬HomogeneousIdeal.irrelevant ğ’œ â‰¤ carrier.asHomogeneousIdeal f_deg hm q :=
  fun rid => carrier.denom_not_mem f_deg hm q <| rid <| DirectSum.decompose_of_mem_ne ğ’œ f_deg hm.ne'
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.relevant AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.relevant

theorem carrier.asIdeal.ne_top : carrier.asIdeal f_deg hm q â‰  âŠ¤ := fun rid =>
  carrier.denom_not_mem f_deg hm q (rid.symm â–¸ Submodule.mem_top)
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.as_ideal.ne_top AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal.ne_top

theorem carrier.asIdeal.prime : (carrier.asIdeal f_deg hm q).IsPrime :=
  (carrier.asIdeal.homogeneous f_deg hm q).isPrime_of_homogeneous_mem_or_mem
    (carrier.asIdeal.ne_top f_deg hm q) fun {x y} âŸ¨nx, hnxâŸ© âŸ¨ny, hnyâŸ© hxy =>
    show (âˆ€ i, _ âˆˆ _) âˆ¨ âˆ€ i, _ âˆˆ _ by
      rw [â† and_forall_ne nx, and_iff_left, â† and_forall_ne ny, and_iff_left]
      Â· apply q.2.mem_or_mem; convert hxy (nx + ny) using 1
        dsimp
        simp_rw [decompose_of_mem_same ğ’œ hnx, decompose_of_mem_same ğ’œ hny,
          decompose_of_mem_same ğ’œ (SetLike.GradedMonoid.toGradedMul.mul_mem hnx hny),
          mul_pow, pow_add]
        simp only [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
          HomogeneousLocalization.mul_val, mk_mul]
        simp only [Submonoid.mk_mul_mk, mk_eq_monoidOf_mk']
      all_goals
        intro n hn; convert q.1.zero_mem using 1
        rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
          HomogeneousLocalization.zero_val]; simp_rw [proj_apply]
        convert mk_zero (S := Submonoid.powers f) _
        rw [decompose_of_mem_ne ğ’œ _ hn.symm, zero_pow hm.ne']
        Â· first | exact hnx | exact hny
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.as_ideal.prime AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal.prime

/-- The function `Spec Aâ°_f â†’ Proj|D(f)` sending `q` to `{a | aáµ¢áµ/fâ± âˆˆ q}`. -/
def toFun : (Spec.T Aâ°_ f) â†’ Proj.T| pbo f := fun q =>
  âŸ¨âŸ¨carrier.asHomogeneousIdeal f_deg hm q, carrier.asIdeal.prime f_deg hm q,
      carrier.relevant f_deg hm qâŸ©,
    (ProjectiveSpectrum.mem_basicOpen _ f _).mp <| carrier.denom_not_mem f_deg hm qâŸ©
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.to_fun AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.toFun

end FromSpec

section toSpecFromSpec

lemma toSpec_fromSpec {f : A} {m : â„•} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) (x : Spec.T (Aâ°_ f)) :
    toSpec ğ’œ f (FromSpec.toFun f_deg hm x) = x := show _ = (_ : PrimeSpectrum _) by
  ext (z : Aâ°_ f); fconstructor <;> intro hz
  Â· change z âˆˆ ToSpec.carrier _ at hz
    erw [ToSpec.carrier_eq_span, mem_span_set] at hz
    obtain âŸ¨c, support_le, rflâŸ© := hz
    refine Ideal.sum_mem _ fun j hj â†¦ Ideal.mul_mem_left _ _ ?_
    obtain âŸ¨s, _, hs, n, s_mem, F_mem, âŸ¨k, rflâŸ©, rflâŸ© := support_le hj
    refine x.IsPrime.mem_of_pow_mem m <| show Quotient.mk'' âŸ¨_, âŸ¨s^m, _âŸ©, _, _âŸ© âˆˆ x.asIdeal from ?_
    convert hs n using 1
    rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
      HomogeneousLocalization.val_mk'']
    simp only [smul_eq_mul, SetLike.coe_gnpow, GradedAlgebra.proj_apply,
      DirectSum.decompose_of_mem_same ğ’œ s_mem]
    by_cases ineq : f^k = 0
    Â· have := IsLocalization.uniqueOfZeroMem (M := Submonoid.powers f) (S := Localization.Away f)
        âŸ¨k, ineqâŸ©
      exact Subsingleton.elim _ _
    Â· simp_rw [â† pow_mul]; congr
      exact DirectSum.degree_eq_of_mem_mem ğ’œ F_mem (SetLike.pow_mem_graded k f_deg) ineq |>.symm

  Â· erw [ToSpec.mem_carrier_iff]
    obtain âŸ¨k, (k_spec : f^k = z.den)âŸ© := z.den_mem
    rw [show z.val = (Localization.mk z.num âŸ¨f^k, âŸ¨k, rflâŸ©âŸ© : Away f) by
        Â· rw [z.eq_num_div_den]; congr; exact k_spec.symm,
      show (mk z.num âŸ¨f^k, âŸ¨k, rflâŸ©âŸ© : Away f) = mk z.num 1 * (mk 1 âŸ¨f^k, âŸ¨k, rflâŸ©âŸ© : Away f) by
        Â· rw [mk_mul, mul_one, one_mul]]
    refine Ideal.mul_mem_right _ _ <| Ideal.subset_span âŸ¨z.num, ?_, rflâŸ©

    intro j
    by_cases ineq : z.deg = j
    Â· subst ineq
      simp only [CommRingCat.coe_of, GradedAlgebra.proj_apply,
        DirectSum.decompose_of_mem_same ğ’œ z.num_mem_deg]
      convert x.IsPrime.pow_mem_iff_mem m hm |>.mpr hz using 1
      rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
        HomogeneousLocalization.pow_val, z.eq_num_div_den, Localization.mk_pow]
      by_cases ineq : f^k = 0
      Â· have := IsLocalization.uniqueOfZeroMem (M := Submonoid.powers f) (S := Localization.Away f)
          âŸ¨k, ineqâŸ©
        exact Subsingleton.elim _ _
      Â· dsimp; congr 2
        rw [â† k_spec, â† pow_mul, show z.deg = k * m from
          degree_eq_of_mem_mem ğ’œ (k_spec â–¸ z.den_mem_deg) (SetLike.pow_mem_graded k f_deg) ineq]
    Â· simp only [CommRingCat.coe_of, GradedAlgebra.proj_apply, zero_pow (by linarith only [hm]),
        DirectSum.decompose_of_mem_ne ğ’œ z.num_mem_deg ineq]
      convert x.asIdeal.zero_mem
      rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
        HomogeneousLocalization.zero_val, Localization.mk_zero]

@[deprecated] -- 2024-03-02
alias toSpecFromSpec := toSpec_fromSpec

end toSpecFromSpec

section fromSpecToSpec

lemma fromSpec_toSpec {f : A} {m : â„•} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) (x : Proj.T| pbo f) :
    FromSpec.toFun f_deg hm (toSpec ğ’œ f x) = x := by
  classical
  refine Subtype.ext <| ProjectiveSpectrum.ext _ _ <| HomogeneousIdeal.ext <| Ideal.ext fun z â†¦ ?_
  constructor <;> intro hz
  Â· rw [â† DirectSum.sum_support_decompose ğ’œ z]
    refine Ideal.sum_mem _ fun i _ â†¦ ?_
    obtain âŸ¨c, N, acd, eq1âŸ© := ToSpec.MemCarrier.clear_denominator x (hz i)
    rw [HomogeneousLocalization.val_mk'', smul_mk, â† mk_one_eq_algebraMap, mk_eq_mk_iff,
      r_iff_exists, OneMemClass.coe_one, one_mul] at eq1
    obtain âŸ¨âŸ¨_, âŸ¨k, rflâŸ©âŸ©, eq1âŸ© := eq1
    dsimp at eq1
    rw [â† mul_assoc, â† mul_assoc, â† pow_add, â† pow_add] at eq1
    suffices mem : f^(k + i) * âˆ‘ i in c.support.attach, acd (c i) _ * _ âˆˆ
      x.1.asHomogeneousIdeal from
      x.1.isPrime.mem_of_pow_mem _ <| x.1.isPrime.mem_or_mem (eq1.symm â–¸ mem)
        |>.resolve_left fun r â†¦ ProjectiveSpectrum.mem_basicOpen ğ’œ _ _
        |>.mp x.2 <| x.1.isPrime.mem_of_pow_mem _ r
    exact Ideal.mul_mem_left _ _ <| Ideal.sum_mem _ fun i _ â†¦
      Ideal.mul_mem_left _ _ i.1.2.choose_spec.1

  Â· intro i
    erw [ToSpec.mem_carrier_iff, HomogeneousLocalization.val_mk'']
    dsimp only [GradedAlgebra.proj_apply]
    rw [show (mk (decompose ğ’œ z i ^ m) âŸ¨f^i, âŸ¨i, rflâŸ©âŸ© : Away f) =
      (decompose ğ’œ z i ^ m : A) â€¢ (mk 1 âŸ¨f^i, âŸ¨i, rflâŸ©âŸ© : Away f) by
      Â· rw [smul_mk, smul_eq_mul, mul_one], Algebra.smul_def]
    exact Ideal.mul_mem_right _ _ <|
      Ideal.subset_span âŸ¨_, âŸ¨Ideal.pow_mem_of_mem _ (x.1.asHomogeneousIdeal.2 i hz) _ hm, rflâŸ©âŸ©

lemma toSpec_injective {f : A} {m : â„•} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) :
    Function.Injective (toSpec ğ’œ f) := by
  intro xâ‚ xâ‚‚ h
  have := congr_arg (FromSpec.toFun f_deg hm) h
  rwa [fromSpec_toSpec, fromSpec_toSpec] at this

lemma toSpec_surjective {f : A} {m : â„•} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) :
    Function.Surjective (toSpec ğ’œ f) :=
  Function.surjective_iff_hasRightInverse |>.mpr
    âŸ¨FromSpec.toFun f_deg hm, toSpec_fromSpec ğ’œ f_deg hmâŸ©

lemma toSpec_bijective {f : A} {m : â„•} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m):
    Function.Bijective (toSpec (ğ’œ := ğ’œ) (f := f)) :=
  âŸ¨toSpec_injective ğ’œ f_deg hm, toSpec_surjective ğ’œ f_deg hmâŸ©

end fromSpecToSpec

namespace toSpec

variable {f : A} {m : â„•} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m)

variable {ğ’œ} in
lemma image_basicOpen_eq_basicOpen (a : A) (i : â„•) :
    toSpec ğ’œ f '' {x | x.1 âˆˆ (pbo f) âŠ“ pbo (decompose ğ’œ a i)} =
    (PrimeSpectrum.basicOpen (R := Aâ°_ f) <|
      Quotient.mk'' âŸ¨m * i, âŸ¨decompose ğ’œ a i ^ m, SetLike.pow_mem_graded _ (Submodule.coe_mem _)âŸ©,
        âŸ¨f^i, by rw [mul_comm]; exact SetLike.pow_mem_graded _ f_degâŸ©, âŸ¨i, rflâŸ©âŸ©).1 :=
  Set.preimage_injective.mpr (toSpec_surjective ğ’œ f_deg hm) <|
    Set.preimage_image_eq _ (toSpec_injective ğ’œ f_deg hm) â–¸ by
  rw [Opens.carrier_eq_coe, toSpec_preimage_eq, ProjectiveSpectrum.basicOpen_pow ğ’œ _ m hm]

end toSpec

variable {ğ’œ} in
/--The continuous function `Spec Aâ°_f â†’ Proj|D(f)` by sending `q` to `{a | aáµ¢áµ/fâ± âˆˆ q}` where
`m` is the degree of `f`.-/
def fromSpec {f : A} {m : â„•} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) :
    (Spec.T (Aâ°_ f)) âŸ¶ (Proj.T| (pbo f)) where
  toFun := FromSpec.toFun f_deg hm
  continuous_toFun := by
    rw [isTopologicalBasis_subtype (ProjectiveSpectrum.isTopologicalBasis_basic_opens ğ’œ) (pbo f).1
      |>.continuous_iff]
    rintro s âŸ¨_, âŸ¨a, rflâŸ©, rflâŸ©
    have hâ‚ : Subtype.val (p := (pbo f).1) â»Â¹' (pbo a) =
        â‹ƒ i : â„•, {x | x.1 âˆˆ (pbo f) âŠ“ pbo (decompose ğ’œ a i)} := by
      ext âŸ¨x, (hx : f âˆ‰ x.asHomogeneousIdeal)âŸ©
      rw [ProjectiveSpectrum.basicOpen_eq_union_of_projection ğ’œ a]
      change (âˆƒ _, _) â†” (âˆƒ _, _)
      simp only [GradedAlgebra.proj_apply, Set.mem_range, Set.iUnion_exists, exists_exists_eq_and,
        Set.mem_iUnion, SetLike.mem_coe, exists_prop, exists_and_right,
        ProjectiveSpectrum.mem_basicOpen, Opens.carrier_eq_coe, Set.mem_setOf_eq]
      exact exists_congr fun n â†¦ show _ â†” (_ âˆ‰ _ âˆ§ _ âˆ‰ _) by tauto

    let e : _ â‰ƒ _ :=
      âŸ¨FromSpec.toFun f_deg hm, ToSpec.toFun f, toSpec_fromSpec _ _ _, fromSpec_toSpec _ _ _âŸ©
    change IsOpen <| e â»Â¹' _
    rw [Set.preimage_equiv_eq_image_symm, hâ‚, Set.image_iUnion]
    exact isOpen_iUnion fun i â†¦ toSpec.image_basicOpen_eq_basicOpen f_deg hm a i â–¸
      PrimeSpectrum.isOpen_basicOpen

end ProjIsoSpecTopComponent

variable {ğ’œ} in
/--
The homeomorphism `Proj|D(f) â‰… Spec Aâ°_f` defined by
- `Ï† : Proj|D(f) âŸ¶ Spec Aâ°_f` by sending `x` to `Aâ°_f âˆ© span {g / 1 | g âˆˆ x}`
- `Ïˆ : Spec Aâ°_f âŸ¶ Proj|D(f)` by sending `q` to `{a | aáµ¢áµ/fâ± âˆˆ q}`.
-/
def projIsoSpecTopComponent {f : A} {m : â„•} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) :
    (Proj.T| (pbo f)) â‰… (Spec.T (Aâ°_ f))  where
  hom := ProjIsoSpecTopComponent.toSpec ğ’œ f
  inv := ProjIsoSpecTopComponent.fromSpec f_deg hm
  hom_inv_id := ConcreteCategory.hom_ext _ _
    (ProjIsoSpecTopComponent.fromSpec_toSpec ğ’œ f_deg hm)
  inv_hom_id := ConcreteCategory.hom_ext _ _
    (ProjIsoSpecTopComponent.toSpec_fromSpec ğ’œ f_deg hm)

end AlgebraicGeometry
