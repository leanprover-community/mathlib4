/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Andrew Yang
-/
import Mathlib.AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf
import Mathlib.AlgebraicGeometry.GammaSpecAdjunction
import Mathlib.RingTheory.GradedAlgebra.Radical

/-!
# Proj as a scheme

This file is to prove that `Proj` is a scheme.

## Notation

* `Proj`      : `Proj` as a locally ringed space
* `Proj.T`    : the underlying topological space of `Proj`
* `Proj| U`   : `Proj` restricted to some open set `U`
* `Proj.T| U` : the underlying topological space of `Proj` restricted to open set `U`
* `pbo f`     : basic open set at `f` in `Proj`
* `Spec`      : `Spec` as a locally ringed space
* `Spec.T`    : the underlying topological space of `Spec`
* `sbo g`     : basic open set at `g` in `Spec`
* `Aâ°_x`      : the degree zero part of localized ring `Aâ‚“`

## Implementation

In `AlgebraicGeometry/ProjectiveSpectrum/StructureSheaf.lean`, we have given `Proj` a
structure sheaf so that `Proj` is a locally ringed space. In this file we will prove that `Proj`
equipped with this structure sheaf is a scheme. We achieve this by using an affine cover by basic
open sets in `Proj`, more specifically:

1. We prove that `Proj` can be covered by basic open sets at homogeneous elements of positive
    degree.
2. We prove that for any homogeneous element `f : A` of positive degree `m`, `Proj.T | (pbo f)` is
    homeomorphic to `Spec.T Aâ°_f`:
  - forward direction `toSpec`:
    for any `x : pbo f`, i.e. a relevant homogeneous prime ideal `x`, send it to
    `Aâ°_f âˆ© span {g / 1 | g âˆˆ x}` (see `ProjIsoSpecTopComponent.ToSpec.carrier`). This ideal is
    prime, the proof is in `ProjIsoSpecTopComponent.ToSpec.toFun`. The fact that this function
    is continuous is found in `ProjIsoSpecTopComponent.toSpec`
  - backward direction `fromSpec`:
    for any `q : Spec Aâ°_f`, we send it to `{a | âˆ€ i, aáµ¢áµ/fâ± âˆˆ q}`; we need this to be a
    homogeneous prime ideal that is relevant.
    * This is in fact an ideal, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal`;
    * This ideal is also homogeneous, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal.homogeneous`;
    * This ideal is relevant, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.relevant`;
    * This ideal is prime, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal.prime`.
    Hence we have a well-defined function `Spec.T Aâ°_f â†’ Proj.T | (pbo f)`, this function is called
    `ProjIsoSpecTopComponent.FromSpec.toFun`. But to prove the continuity of this function, we need
    to prove `fromSpec âˆ˜ toSpec` and `toSpec âˆ˜ fromSpec` are both identities; these are achieved in
    `ProjIsoSpecTopComponent.fromSpec_toSpec` and `ProjIsoSpecTopComponent.toSpec_fromSpec`.
3. Then we construct a morphism of locally ringed spaces `Î± : Proj| (pbo f) âŸ¶ Spec.T Aâ°_f` as the
    following: by the Gamma-Spec adjunction, it is sufficient to construct a ring map
    `Aâ°_f â†’ Î“(Proj, pbo f)` from the ring of homogeneous localization of `A` away from `f` to the
    local sections of structure sheaf of projective spectrum on the basic open set around `f`.
    The map `Aâ°_f â†’ Î“(Proj, pbo f)` is constructed in `awayToÎ“` and is defined by sending
    `s âˆˆ Aâ°_f` to the section `x â†¦ s` on `pbo f`.

## Main Definitions and Statements

For a homogeneous element `f` of degree `m`
* `ProjIsoSpecTopComponent.toSpec`: the continuous map between `Proj.T| pbo f` and `Spec.T Aâ°_f`
  defined by sending `x : Proj| (pbo f)` to `Aâ°_f âˆ© span {g / 1 | g âˆˆ x}`. We also denote this map
  as `Ïˆ`.
* `ProjIsoSpecTopComponent.ToSpec.preimage_eq`: for any `a: A`, if `a/f^m` has degree zero,
  then the preimage of `sbo a/f^m` under `toSpec f` is `pbo f âˆ© pbo a`.

If we further assume `m` is positive
* `ProjIsoSpecTopComponent.fromSpec`: the continuous map between `Spec.T Aâ°_f` and `Proj.T| pbo f`
  defined by sending `q` to `{a | aáµ¢áµ/fâ± âˆˆ q}` where `aáµ¢` is the `i`-th coordinate of `a`.
  We also denote this map as `Ï†`
* `projIsoSpecTopComponent`: the homeomorphism `Proj.T| pbo f â‰… Spec.T Aâ°_f` obtained by `Ï†` and
  `Ïˆ`.
* `ProjectiveSpectrum.Proj.toSpec`: the morphism of locally ringed spaces between `Proj| pbo f`
  and `Spec Aâ°_f` corresponding to the ring map `Aâ°_f â†’ Î“(Proj, pbo f)` under the Gamma-Spec
  adjunction defined by sending `s` to the section `x â†¦ s` on `pbo f`.

Finally,
* `AlgebraicGeometry.Proj`: for any `â„•`-graded ring `A`, `Proj A` is locally affine, hence is a
  scheme.

## Reference
* [Robin Hartshorne, *Algebraic Geometry*][Har77]: Chapter II.2 Proposition 2.5
-/

noncomputable section


namespace AlgebraicGeometry

open scoped DirectSum Pointwise

open DirectSum SetLike.GradedMonoid Localization

open Finset hiding mk_zero

variable {R A : Type*}
variable [CommRing R] [CommRing A] [Algebra R A]
variable (ğ’œ : â„• â†’ Submodule R A)
variable [GradedAlgebra ğ’œ]

open TopCat TopologicalSpace

open CategoryTheory Opposite

open ProjectiveSpectrum.StructureSheaf

-- Porting note: currently require lack of hygiene to use in variable declarations
-- maybe all make into notation3?
set_option hygiene false
/-- `Proj` as a locally ringed space -/
local notation3 "Proj" => Proj.toLocallyRingedSpace ğ’œ

/-- The underlying topological space of `Proj` -/
local notation3 "Proj.T" => PresheafedSpace.carrier <| SheafedSpace.toPresheafedSpace
  <| LocallyRingedSpace.toSheafedSpace <| Proj.toLocallyRingedSpace ğ’œ

/-- `Proj` restrict to some open set -/
macro "Proj| " U:term : term =>
  `((Proj.toLocallyRingedSpace ğ’œ).restrict
    (Opens.isOpenEmbedding (X := Proj.T) ($U : Opens Proj.T)))

/-- the underlying topological space of `Proj` restricted to some open set -/
local notation "Proj.T| " U => PresheafedSpace.carrier <| SheafedSpace.toPresheafedSpace
  <| LocallyRingedSpace.toSheafedSpace
    <| (LocallyRingedSpace.restrict Proj (Opens.isOpenEmbedding (X := Proj.T) (U : Opens Proj.T)))

/-- basic open sets in `Proj` -/
local notation "pbo " x => ProjectiveSpectrum.basicOpen ğ’œ x

/-- basic open sets in `Spec` -/
local notation "sbo " f => PrimeSpectrum.basicOpen f

/-- `Spec` as a locally ringed space -/
local notation3 "Spec " ring => Spec.locallyRingedSpaceObj (CommRingCat.of ring)

/-- the underlying topological space of `Spec` -/
local notation "Spec.T " ring =>
  (Spec.locallyRingedSpaceObj (CommRingCat.of ring)).toSheafedSpace.toPresheafedSpace.1

local notation3 "Aâ°_ " f => HomogeneousLocalization.Away ğ’œ f

namespace ProjIsoSpecTopComponent

/-
This section is to construct the homeomorphism between `Proj` restricted at basic open set at
a homogeneous element `x` and `Spec Aâ°â‚“` where `Aâ°â‚“` is the degree zero part of the localized
ring `Aâ‚“`.
-/
namespace ToSpec

open Ideal

-- This section is to construct the forward direction :
-- So for any `x` in `Proj| (pbo f)`, we need some point in `Spec Aâ°_f`, i.e. a prime ideal,
-- and we need this correspondence to be continuous in their Zariski topology.
variable {ğ’œ}
variable {f : A} {m : â„•} (x : Proj| (pbo f))

/--
For any `x` in `Proj| (pbo f)`, the corresponding ideal in `Spec Aâ°_f`. This fact that this ideal
is prime is proven in `TopComponent.Forward.toFun`. -/
def carrier : Ideal (Aâ°_ f) :=
  Ideal.comap (algebraMap (Aâ°_ f) (Away f))
    (x.val.asHomogeneousIdeal.toIdeal.map (algebraMap A (Away f)))

@[simp]
theorem mk_mem_carrier (z : HomogeneousLocalization.NumDenSameDeg ğ’œ (.powers f)) :
    HomogeneousLocalization.mk z âˆˆ carrier x â†” z.num.1 âˆˆ x.1.asHomogeneousIdeal := by
  rw [carrier, Ideal.mem_comap, HomogeneousLocalization.algebraMap_apply,
    HomogeneousLocalization.val_mk, Localization.mk_eq_mk', IsLocalization.mk'_eq_mul_mk'_one,
    mul_comm, Ideal.unit_mul_mem_iff_mem, â† Ideal.mem_comap,
    IsLocalization.comap_map_of_isPrime_disjoint (.powers f)]
  Â· rfl
  Â· infer_instance
  Â· exact (disjoint_powers_iff_notMem _ (Ideal.IsPrime.isRadical inferInstance)).mpr x.2
  Â· exact isUnit_of_invertible _

theorem isPrime_carrier : Ideal.IsPrime (carrier x) := by
  refine Ideal.IsPrime.comap _ (hK := ?_)
  exact IsLocalization.isPrime_of_isPrime_disjoint
    (Submonoid.powers f) _ _ inferInstance
    ((disjoint_powers_iff_notMem _ (Ideal.IsPrime.isRadical inferInstance)).mpr x.2)

variable (f)

/-- The function between the basic open set `D(f)` in `Proj` to the corresponding basic open set in
`Spec Aâ°_f`. This is bundled into a continuous map in `TopComponent.forward`.
-/
@[simps -isSimp]
def toFun (x : Proj.T| pbo f) : Spec.T Aâ°_ f :=
  âŸ¨carrier x, isPrime_carrier xâŸ©

/-
The preimage of basic open set `D(a/f^n)` in `Spec Aâ°_f` under the forward map from `Proj A` to
`Spec Aâ°_f` is the basic open set `D(a) âˆ© D(f)` in `Proj A`. This lemma is used to prove that the
forward map is continuous.
-/
theorem preimage_basicOpen (z : HomogeneousLocalization.NumDenSameDeg ğ’œ (.powers f)) :
    toFun f â»Â¹' (sbo (HomogeneousLocalization.mk z) : Set (PrimeSpectrum (Aâ°_ f))) =
      Subtype.val â»Â¹' (pbo z.num.1 : Set (ProjectiveSpectrum ğ’œ)) :=
  Set.ext fun y â†¦ (mk_mem_carrier y z).not

end ToSpec

section

/-- The continuous function from the basic open set `D(f)` in `Proj`
to the corresponding basic open set in `Spec Aâ°_f`. -/
@[simps! -isSimp hom_apply_asIdeal]
def toSpec (f : A) : (Proj.T| pbo f) âŸ¶ Spec.T Aâ°_ f :=
  TopCat.ofHom
  { toFun := ToSpec.toFun f
    continuous_toFun := by
      rw [PrimeSpectrum.isTopologicalBasis_basic_opens.continuous_iff]
      rintro _ âŸ¨x, rflâŸ©
      obtain âŸ¨x, rflâŸ© := Quotient.mk''_surjective x
      rw [ToSpec.preimage_basicOpen]
      exact (pbo x.num).2.preimage continuous_subtype_val }

variable {ğ’œ} in
lemma toSpec_preimage_basicOpen {f} (z : HomogeneousLocalization.NumDenSameDeg ğ’œ (.powers f)) :
    toSpec ğ’œ f â»Â¹' (sbo (HomogeneousLocalization.mk z) : Set (PrimeSpectrum (Aâ°_ f))) =
      Subtype.val â»Â¹' (pbo z.num.1 : Set (ProjectiveSpectrum ğ’œ)) :=
  ToSpec.preimage_basicOpen f z

end

namespace FromSpec

open GradedAlgebra SetLike

open Finset hiding mk_zero

open HomogeneousLocalization

variable {ğ’œ}
variable {f : A} {m : â„•} (f_deg : f âˆˆ ğ’œ m)

open Lean Meta Elab Tactic

macro "mem_tac_aux" : tactic =>
  `(tactic| first | exact pow_mem_graded _ (Submodule.coe_mem _) | exact natCast_mem_graded _ _ |
    exact pow_mem_graded _ f_deg)

macro "mem_tac" : tactic =>
  `(tactic| first | mem_tac_aux |
    repeat (all_goals (apply SetLike.GradedMonoid.toGradedMul.mul_mem)); mem_tac_aux)

/-- The function from `Spec Aâ°_f` to `Proj|D(f)` is defined by `q â†¦ {a | aáµ¢áµ/fâ± âˆˆ q}`, i.e. sending
`q` a prime ideal in `Aâ°_f` to the homogeneous prime relevant ideal containing only and all the
elements `a : A` such that for every `i`, the degree 0 element formed by dividing the `m`-th power
of the `i`-th projection of `a` by the `i`-th power of the degree-`m` homogeneous element `f`,
lies in `q`.

The set `{a | aáµ¢áµ/fâ± âˆˆ q}`
* is an ideal, as proved in `carrier.asIdeal`;
* is homogeneous, as proved in `carrier.asHomogeneousIdeal`;
* is prime, as proved in `carrier.asIdeal.prime`;
* is relevant, as proved in `carrier.relevant`.
-/
def carrier (f_deg : f âˆˆ ğ’œ m) (q : Spec.T Aâ°_ f) : Set A :=
  {a | âˆ€ i, (HomogeneousLocalization.mk âŸ¨m * i, âŸ¨proj ğ’œ i a ^ m, by rw [â† smul_eq_mul]; mem_tacâŸ©,
              âŸ¨f ^ i, by rw [mul_comm]; mem_tacâŸ©, âŸ¨_, rflâŸ©âŸ© : Aâ°_ f) âˆˆ q.1}

theorem mem_carrier_iff (q : Spec.T Aâ°_ f) (a : A) :
    a âˆˆ carrier f_deg q â†” âˆ€ i, (HomogeneousLocalization.mk âŸ¨m * i, âŸ¨proj ğ’œ i a ^ m, by
      rw [â† smul_eq_mul]; mem_tacâŸ©,
      âŸ¨f ^ i, by rw [mul_comm]; mem_tacâŸ©, âŸ¨_, rflâŸ©âŸ© : Aâ°_ f) âˆˆ q.1 :=
  Iff.rfl

theorem mem_carrier_iff' (q : Spec.T Aâ°_ f) (a : A) :
    a âˆˆ carrier f_deg q â†”
      âˆ€ i, (Localization.mk (proj ğ’œ i a ^ m) âŸ¨f ^ i, âŸ¨i, rflâŸ©âŸ© : Localization.Away f) âˆˆ
          algebraMap (HomogeneousLocalization.Away ğ’œ f) (Localization.Away f) '' { s | s âˆˆ q.1 } :=
  (mem_carrier_iff f_deg q a).trans
    (by
      constructor <;> intro h i <;> specialize h i
      Â· rw [Set.mem_image]; refine âŸ¨_, h, rflâŸ©
      Â· rw [Set.mem_image] at h; rcases h with âŸ¨x, h, hxâŸ©
        change x âˆˆ q.asIdeal at h
        convert h
        rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk]
        dsimp only [Subtype.coe_mk]; rw [â† hx]; rfl)

theorem mem_carrier_iff_of_mem (hm : 0 < m) (q : Spec.T Aâ°_ f) (a : A) {n} (hn : a âˆˆ ğ’œ n) :
    a âˆˆ carrier f_deg q â†”
      (HomogeneousLocalization.mk âŸ¨m * n, âŸ¨a ^ m, pow_mem_graded m hnâŸ©,
        âŸ¨f ^ n, by rw [mul_comm]; mem_tacâŸ©, âŸ¨_, rflâŸ©âŸ© : Aâ°_ f) âˆˆ q.asIdeal := by
  trans (HomogeneousLocalization.mk âŸ¨m * n, âŸ¨proj ğ’œ n a ^ m, by rw [â† smul_eq_mul]; mem_tacâŸ©,
    âŸ¨f ^ n, by rw [mul_comm]; mem_tacâŸ©, âŸ¨_, rflâŸ©âŸ© : Aâ°_ f) âˆˆ q.asIdeal
  Â· refine âŸ¨fun h â†¦ h n, fun h i â†¦ if hi : i = n then hi â–¸ h else ?_âŸ©
    convert zero_mem q.asIdeal
    apply HomogeneousLocalization.val_injective
    simp only [proj_apply, decompose_of_mem_ne _ hn (Ne.symm hi), zero_pow hm.ne',
      HomogeneousLocalization.val_mk, Localization.mk_zero, HomogeneousLocalization.val_zero]
  Â· simp only [proj_apply, decompose_of_mem_same _ hn]

theorem mem_carrier_iff_of_mem_mul (hm : 0 < m)
    (q : Spec.T Aâ°_ f) (a : A) {n} (hn : a âˆˆ ğ’œ (n * m)) :
    a âˆˆ carrier f_deg q â†” (HomogeneousLocalization.mk âŸ¨m * n, âŸ¨a, mul_comm n m â–¸ hnâŸ©,
        âŸ¨f ^ n, by rw [mul_comm]; mem_tacâŸ©, âŸ¨_, rflâŸ©âŸ© : Aâ°_ f) âˆˆ q.asIdeal := by
  rw [mem_carrier_iff_of_mem f_deg hm q a hn, iff_iff_eq, eq_comm,
    â† Ideal.IsPrime.pow_mem_iff_mem (Î± := Aâ°_ f) inferInstance m hm]
  congr 1
  apply HomogeneousLocalization.val_injective
  simp only [HomogeneousLocalization.val_mk, HomogeneousLocalization.val_pow,
    Localization.mk_pow, pow_mul]
  rfl

theorem num_mem_carrier_iff (hm : 0 < m) (q : Spec.T Aâ°_ f)
    (z : HomogeneousLocalization.NumDenSameDeg ğ’œ (.powers f)) :
    z.num.1 âˆˆ carrier f_deg q â†” HomogeneousLocalization.mk z âˆˆ q.asIdeal := by
  obtain âŸ¨n, hn : f ^ n = _âŸ© := z.den_mem
  have : f ^ n â‰  0 := fun e â†¦ by
    have := HomogeneousLocalization.subsingleton ğ’œ (x := .powers f) âŸ¨n, eâŸ©
    exact IsEmpty.elim (inferInstanceAs (IsEmpty (PrimeSpectrum (Aâ°_ f)))) q
  convert mem_carrier_iff_of_mem_mul f_deg hm q z.num.1 (n := n) ?_ using 2
  Â· apply HomogeneousLocalization.val_injective; simp only [hn, HomogeneousLocalization.val_mk]
  Â· have := degree_eq_of_mem_mem ğ’œ (SetLike.pow_mem_graded n f_deg) (hn.symm â–¸ z.den.2) this
    rw [â† smul_eq_mul, this]; exact z.num.2

theorem carrier.add_mem (q : Spec.T Aâ°_ f) {a b : A} (ha : a âˆˆ carrier f_deg q)
    (hb : b âˆˆ carrier f_deg q) : a + b âˆˆ carrier f_deg q := by
  refine fun i => (q.2.mem_or_mem ?_).elim id id
  change (HomogeneousLocalization.mk âŸ¨_, _, _, _âŸ© : Aâ°_ f) âˆˆ q.1; dsimp only [Subtype.coe_mk]
  simp_rw [â† pow_add, map_add, add_pow, mul_comm, â† nsmul_eq_mul]
  let g : â„• â†’ Aâ°_ f := fun j => (m + m).choose j â€¢
      if h2 : m + m < j then (0 : Aâ°_ f)
      else
        if h1 : j â‰¤ m then
          (HomogeneousLocalization.mk
            âŸ¨m * i, âŸ¨proj ğ’œ i a ^ j * proj ğ’œ i b ^ (m - j), ?_âŸ©,
              âŸ¨_, by rw [mul_comm]; mem_tacâŸ©, âŸ¨i, rflâŸ©âŸ© : Aâ°_ f) *
          (HomogeneousLocalization.mk
            âŸ¨m * i, âŸ¨proj ğ’œ i b ^ m, by rw [â† smul_eq_mul]; mem_tacâŸ©,
              âŸ¨_, by rw [mul_comm]; mem_tacâŸ©, âŸ¨i, rflâŸ©âŸ© : Aâ°_ f)
        else
          (HomogeneousLocalization.mk
            âŸ¨m * i, âŸ¨proj ğ’œ i a ^ m, by rw [â† smul_eq_mul]; mem_tacâŸ©,
              âŸ¨_, by rw [mul_comm]; mem_tacâŸ©, âŸ¨i, rflâŸ©âŸ© : Aâ°_ f) *
          (HomogeneousLocalization.mk
            âŸ¨m * i, âŸ¨proj ğ’œ i a ^ (j - m) * proj ğ’œ i b ^ (m + m - j), ?_âŸ©,
              âŸ¨_, by rw [mul_comm]; mem_tacâŸ©, âŸ¨i, rflâŸ©âŸ© : Aâ°_ f)
  rotate_left
  Â· rw [(_ : m * i = _)]
    apply GradedMonoid.toGradedMul.mul_mem <;> mem_tac_aux
    rw [â† add_smul, Nat.add_sub_of_le h1]; rfl
  Â· rw [(_ : m * i = _)]
    apply GradedMonoid.toGradedMul.mul_mem (i := (j-m) â€¢ i) (j := (m + m - j) â€¢ i) <;> mem_tac_aux
    rw [â† add_smul]; congr; cutsat
  convert_to âˆ‘ i âˆˆ range (m + m + 1), g i âˆˆ q.1; swap
  Â· refine q.1.sum_mem fun j _ => nsmul_mem ?_ _; split_ifs
    exacts [q.1.zero_mem, q.1.mul_mem_left _ (hb i), q.1.mul_mem_right _ (ha i)]
  rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk]
  change _ = (algebraMap (HomogeneousLocalization.Away ğ’œ f) (Localization.Away f)) _
  dsimp only [Subtype.coe_mk]; rw [map_sum, mk_sum]
  apply Finset.sum_congr rfl fun j hj => _
  intro j hj
  change _ = HomogeneousLocalization.val _
  rw [HomogeneousLocalization.val_smul]
  split_ifs with h2 h1
  Â· exact ((Finset.mem_range.1 hj).not_ge h2).elim
  all_goals simp only [HomogeneousLocalization.val_mul,
    HomogeneousLocalization.val_mk, Localization.mk_mul, â† smul_mk]; congr 2
  Â· dsimp; rw [mul_assoc, â† pow_add, add_comm (m - j), Nat.add_sub_assoc h1]
  Â· simp_rw [pow_add]; rfl
  Â· dsimp; rw [â† mul_assoc, â† pow_add, Nat.add_sub_of_le (le_of_not_ge h1)]
  Â· simp_rw [pow_add]; rfl

variable (hm : 0 < m) (q : Spec.T Aâ°_ f)
include hm

theorem carrier.zero_mem : (0 : A) âˆˆ carrier f_deg q := fun i => by
  convert Submodule.zero_mem q.1 using 1
  rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk,
    HomogeneousLocalization.val_zero]; simp_rw [map_zero, zero_pow hm.ne']
  convert Localization.mk_zero (S := Submonoid.powers f) _ using 1

theorem carrier.smul_mem (c x : A) (hx : x âˆˆ carrier f_deg q) : c â€¢ x âˆˆ carrier f_deg q := by
  revert c
  refine DirectSum.Decomposition.inductionOn ğ’œ ?_ ?_ ?_
  Â· rw [zero_smul]; exact carrier.zero_mem f_deg hm _
  Â· rintro n âŸ¨a, haâŸ© i
    simp_rw [proj_apply, smul_eq_mul, coe_decompose_mul_of_left_mem ğ’œ i ha]
    let product : Aâ°_ f :=
      (HomogeneousLocalization.mk
          âŸ¨_, âŸ¨a ^ m, pow_mem_graded m haâŸ©, âŸ¨_, ?_âŸ©, âŸ¨n, rflâŸ©âŸ© : Aâ°_ f) *
        (HomogeneousLocalization.mk
          âŸ¨_, âŸ¨proj ğ’œ (i - n) x ^ m, by mem_tacâŸ©, âŸ¨_, ?_âŸ©, âŸ¨i - n, rflâŸ©âŸ© : Aâ°_ f)
    Â· split_ifs with h
      Â· convert_to product âˆˆ q.1
        Â· dsimp [product]
          rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk,
            HomogeneousLocalization.val_mul, HomogeneousLocalization.val_mk,
            HomogeneousLocalization.val_mk]
          Â· simp_rw [mul_pow]; rw [Localization.mk_mul]
            Â· congr; rw [â† pow_add, Nat.add_sub_of_le h]
        Â· apply Ideal.mul_mem_left (Î± := Aâ°_ f) _ _ (hx _)
          rw [(_ : m â€¢ n = _)]
          Â· mem_tac
          Â· simp only [smul_eq_mul, mul_comm]
      Â· simpa only [map_zero, zero_pow hm.ne'] using zero_mem f_deg hm q i
    rw [(_ : m â€¢ (i - n) = _)]
    Â· mem_tac
    Â· simp only [smul_eq_mul, mul_comm]
  Â· simp_rw [add_smul]; exact fun _ _ => carrier.add_mem f_deg q

/-- For a prime ideal `q` in `Aâ°_f`, the set `{a | aáµ¢áµ/fâ± âˆˆ q}` as an ideal.
-/
def carrier.asIdeal : Ideal A where
  carrier := carrier f_deg q
  zero_mem' := carrier.zero_mem f_deg hm q
  add_mem' := carrier.add_mem f_deg q
  smul_mem' := carrier.smul_mem f_deg hm q


theorem carrier.asIdeal.homogeneous : (carrier.asIdeal f_deg hm q).IsHomogeneous ğ’œ :=
  fun i a ha j =>
  (em (i = j)).elim (fun h => h â–¸ by simpa only [proj_apply, decompose_coe, of_eq_same] using ha _)
    fun h => by
    simpa only [proj_apply, decompose_of_mem_ne ğ’œ (Submodule.coe_mem (decompose ğ’œ a i)) h,
      zero_pow hm.ne', map_zero] using carrier.zero_mem f_deg hm q j

/-- For a prime ideal `q` in `Aâ°_f`, the set `{a | aáµ¢áµ/fâ± âˆˆ q}` as a homogeneous ideal.
-/
def carrier.asHomogeneousIdeal : HomogeneousIdeal ğ’œ :=
  âŸ¨carrier.asIdeal f_deg hm q, carrier.asIdeal.homogeneous f_deg hm qâŸ©

theorem carrier.denom_notMem : f âˆ‰ carrier.asIdeal f_deg hm q := fun rid =>
  q.isPrime.ne_top <|
    (Ideal.eq_top_iff_one _).mpr
      (by
        convert rid m
        rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_one,
          HomogeneousLocalization.val_mk]
        dsimp
        simp_rw [decompose_of_mem_same _ f_deg]
        simp only [mk_eq_monoidOf_mk', Submonoid.LocalizationMap.mk'_self])

@[deprecated (since := "2025-05-23")] alias carrier.denom_not_mem := carrier.denom_notMem

theorem carrier.relevant : Â¬HomogeneousIdeal.irrelevant ğ’œ â‰¤ carrier.asHomogeneousIdeal f_deg hm q :=
  fun rid => carrier.denom_notMem f_deg hm q <| rid <| DirectSum.decompose_of_mem_ne ğ’œ f_deg hm.ne'

theorem carrier.asIdeal.ne_top : carrier.asIdeal f_deg hm q â‰  âŠ¤ := fun rid =>
  carrier.denom_notMem f_deg hm q (rid.symm â–¸ Submodule.mem_top)

theorem carrier.asIdeal.prime : (carrier.asIdeal f_deg hm q).IsPrime :=
  (carrier.asIdeal.homogeneous f_deg hm q).isPrime_of_homogeneous_mem_or_mem
    (carrier.asIdeal.ne_top f_deg hm q) fun {x y} âŸ¨nx, hnxâŸ© âŸ¨ny, hnyâŸ© hxy =>
    show (âˆ€ _, _ âˆˆ _) âˆ¨ âˆ€ _, _ âˆˆ _ by
      rw [â† and_forall_ne nx, and_iff_left, â† and_forall_ne ny, and_iff_left]
      Â· apply q.2.mem_or_mem; convert hxy (nx + ny) using 1
        dsimp
        simp_rw [decompose_of_mem_same ğ’œ hnx, decompose_of_mem_same ğ’œ hny,
          decompose_of_mem_same ğ’œ (SetLike.GradedMonoid.toGradedMul.mul_mem hnx hny),
          mul_pow, pow_add]
        simp only [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk,
          HomogeneousLocalization.val_mul, Localization.mk_mul]
        simp only [Submonoid.mk_mul_mk, mk_eq_monoidOf_mk']
      all_goals
        intro n hn; convert q.1.zero_mem using 1
        rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk,
          HomogeneousLocalization.val_zero]; simp_rw [proj_apply]
        convert mk_zero (S := Submonoid.powers f) _
        rw [decompose_of_mem_ne ğ’œ _ hn.symm, zero_pow hm.ne']
        Â· first | exact hnx | exact hny

/-- The function `Spec Aâ°_f â†’ Proj|D(f)` sending `q` to `{a | aáµ¢áµ/fâ± âˆˆ q}`. -/
def toFun : (Spec.T Aâ°_ f) â†’ Proj.T| pbo f := fun q =>
  âŸ¨âŸ¨carrier.asHomogeneousIdeal f_deg hm q, carrier.asIdeal.prime f_deg hm q,
      carrier.relevant f_deg hm qâŸ©,
    (ProjectiveSpectrum.mem_basicOpen _ f _).mp <| carrier.denom_notMem f_deg hm qâŸ©

end FromSpec

section toSpecFromSpec

lemma toSpec_fromSpec {f : A} {m : â„•} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) (x : Spec.T (Aâ°_ f)) :
    toSpec ğ’œ f (FromSpec.toFun f_deg hm x) = x := by
  apply PrimeSpectrum.ext
  ext z
  obtain âŸ¨z, rflâŸ© := HomogeneousLocalization.mk_surjective z
  rw [â† FromSpec.num_mem_carrier_iff f_deg hm x]
  exact ToSpec.mk_mem_carrier _ z


end toSpecFromSpec

section fromSpecToSpec

lemma fromSpec_toSpec {f : A} {m : â„•} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) (x : Proj.T| pbo f) :
    FromSpec.toFun f_deg hm (toSpec ğ’œ f x) = x := by
  refine Subtype.ext <| ProjectiveSpectrum.ext <| HomogeneousIdeal.ext' ?_
  intro i z hzi
  refine (FromSpec.mem_carrier_iff_of_mem f_deg hm _ _ hzi).trans ?_
  exact (ToSpec.mk_mem_carrier _ _).trans (x.1.2.pow_mem_iff_mem m hm)

lemma toSpec_injective {f : A} {m : â„•} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) :
    Function.Injective (toSpec ğ’œ f) := by
  intro xâ‚ xâ‚‚ h
  have := congr_arg (FromSpec.toFun f_deg hm) h
  rwa [fromSpec_toSpec, fromSpec_toSpec] at this

lemma toSpec_surjective {f : A} {m : â„•} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) :
    Function.Surjective (toSpec ğ’œ f) :=
  Function.surjective_iff_hasRightInverse |>.mpr
    âŸ¨FromSpec.toFun f_deg hm, toSpec_fromSpec ğ’œ f_deg hmâŸ©

lemma toSpec_bijective {f : A} {m : â„•} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) :
    Function.Bijective (toSpec (ğ’œ := ğ’œ) (f := f)) :=
  âŸ¨toSpec_injective ğ’œ f_deg hm, toSpec_surjective ğ’œ f_deg hmâŸ©

end fromSpecToSpec

namespace toSpec

variable {f : A} {m : â„•} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m)
include hm f_deg

variable {ğ’œ} in
lemma image_basicOpen_eq_basicOpen (a : A) (i : â„•) :
    toSpec ğ’œ f '' (Subtype.val â»Â¹' (pbo (decompose ğ’œ a i) : Set (ProjectiveSpectrum ğ’œ))) =
    (PrimeSpectrum.basicOpen (R := Aâ°_ f) <|
      HomogeneousLocalization.mk
        âŸ¨m * i, âŸ¨decompose ğ’œ a i ^ m,
          smul_eq_mul m i â–¸ SetLike.pow_mem_graded _ (Submodule.coe_mem _)âŸ©,
          âŸ¨f^i, by rw [mul_comm]; exact SetLike.pow_mem_graded _ f_degâŸ©, âŸ¨i, rflâŸ©âŸ©).1 :=
  Set.preimage_injective.mpr (toSpec_surjective ğ’œ f_deg hm) <|
    Set.preimage_image_eq _ (toSpec_injective ğ’œ f_deg hm) â–¸ by
  rw [Opens.carrier_eq_coe, toSpec_preimage_basicOpen, ProjectiveSpectrum.basicOpen_pow ğ’œ _ m hm]

end toSpec

variable {ğ’œ} in
/-- The continuous function `Spec Aâ°_f â†’ Proj|D(f)` sending `q` to `{a | aáµ¢áµ/fâ± âˆˆ q}` where
`m` is the degree of `f` -/
def fromSpec {f : A} {m : â„•} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) :
    (Spec.T (Aâ°_ f)) âŸ¶ (Proj.T| (pbo f)) :=
  TopCat.ofHom
  { toFun := FromSpec.toFun f_deg hm
    continuous_toFun := by
      rw [isTopologicalBasis_subtype (ProjectiveSpectrum.isTopologicalBasis_basic_opens ğ’œ) (pbo f).1
        |>.continuous_iff]
      rintro s âŸ¨_, âŸ¨a, rflâŸ©, rflâŸ©
      have hâ‚ : Subtype.val (p := (pbo f).1) â»Â¹' (pbo a) =
          â‹ƒ i : â„•, Subtype.val (p := (pbo f).1) â»Â¹' (pbo (decompose ğ’œ a i)) := by
        simp [ProjectiveSpectrum.basicOpen_eq_union_of_projection ğ’œ a]
      let e : _ â‰ƒ _ :=
        âŸ¨FromSpec.toFun f_deg hm, ToSpec.toFun f, toSpec_fromSpec _ _ _, fromSpec_toSpec _ _ _âŸ©
      change IsOpen <| e â»Â¹' _
      rw [Set.preimage_equiv_eq_image_symm, hâ‚, Set.image_iUnion]
      exact isOpen_iUnion fun i â†¦ toSpec.image_basicOpen_eq_basicOpen f_deg hm a i â–¸
        PrimeSpectrum.isOpen_basicOpen }

end ProjIsoSpecTopComponent

variable {ğ’œ} in
/--
The homeomorphism `Proj|D(f) â‰… Spec Aâ°_f` defined by
- `Ï† : Proj|D(f) âŸ¶ Spec Aâ°_f` by sending `x` to `Aâ°_f âˆ© span {g / 1 | g âˆˆ x}`
- `Ïˆ : Spec Aâ°_f âŸ¶ Proj|D(f)` by sending `q` to `{a | aáµ¢áµ/fâ± âˆˆ q}`.
-/
def projIsoSpecTopComponent {f : A} {m : â„•} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) :
    (Proj.T| (pbo f)) â‰… (Spec.T (Aâ°_ f))  where
  hom := ProjIsoSpecTopComponent.toSpec ğ’œ f
  inv := ProjIsoSpecTopComponent.fromSpec f_deg hm
  hom_inv_id := ConcreteCategory.hom_ext _ _
    (ProjIsoSpecTopComponent.fromSpec_toSpec ğ’œ f_deg hm)
  inv_hom_id := ConcreteCategory.hom_ext _ _
    (ProjIsoSpecTopComponent.toSpec_fromSpec ğ’œ f_deg hm)

namespace ProjectiveSpectrum.Proj

/--
The ring map from `Aâ°_ f` to the local sections of the structure sheaf of the projective spectrum of
`A` on the basic open set `D(f)` defined by sending `s âˆˆ Aâ°_f` to the section `x â†¦ s` on `D(f)`.
-/
def awayToSection (f) : CommRingCat.of (Aâ°_ f) âŸ¶ (structureSheaf ğ’œ).1.obj (op (pbo f)) :=
  CommRingCat.ofHom
    -- Have to hint `S`, otherwise it gets unfolded to `structureSheafInType`
    -- causing `ext` to fail
    (S := (structureSheaf ğ’œ).1.obj (op (pbo f)))
  { toFun s :=
      âŸ¨fun x â†¦ HomogeneousLocalization.mapId ğ’œ (Submonoid.powers_le.mpr x.2) s, fun x â†¦ by
        obtain âŸ¨s, rflâŸ© := HomogeneousLocalization.mk_surjective s
        obtain âŸ¨n, hn : f ^ n = s.den.1âŸ© := s.den_mem
        exact âŸ¨_, x.2, ğŸ™ _, s.1, s.2, s.3,
          fun x hsx â†¦ x.2 (Ideal.IsPrime.mem_of_pow_mem inferInstance n (hn â–¸ hsx)), fun _ â†¦ rflâŸ©âŸ©
    map_add' _ _ := by ext; simp only [map_add, HomogeneousLocalization.val_add, Proj.add_apply]
    map_mul' _ _ := by ext; simp only [map_mul, HomogeneousLocalization.val_mul, Proj.mul_apply]
    map_zero' := by ext; simp only [map_zero, HomogeneousLocalization.val_zero, Proj.zero_apply]
    map_one' := by ext; simp only [map_one, HomogeneousLocalization.val_one, Proj.one_apply] }

lemma awayToSection_germ (f x hx) :
    awayToSection ğ’œ f â‰« (structureSheaf ğ’œ).presheaf.germ _ x hx =
      CommRingCat.ofHom (HomogeneousLocalization.mapId ğ’œ (Submonoid.powers_le.mpr hx)) â‰«
        (Proj.stalkIso' ğ’œ x).toCommRingCatIso.inv := by
  ext z
  apply (Proj.stalkIso' ğ’œ x).eq_symm_apply.mpr
  apply Proj.stalkIso'_germ

lemma awayToSection_apply (f : A) (x p) :
    (((ProjectiveSpectrum.Proj.awayToSection ğ’œ f).1 x).val p).val =
      IsLocalization.map (M := Submonoid.powers f) (T := p.1.1.toIdeal.primeCompl) _
        (RingHom.id _) (Submonoid.powers_le.mpr p.2) x.val := by
  obtain âŸ¨x, rflâŸ© := HomogeneousLocalization.mk_surjective x
  change (HomogeneousLocalization.mapId ğ’œ _ _).val = _
  dsimp [HomogeneousLocalization.mapId, HomogeneousLocalization.map]
  rw [Localization.mk_eq_mk', Localization.mk_eq_mk', IsLocalization.map_mk']
  rfl

/--
The ring map from `Aâ°_ f` to the global sections of the structure sheaf of the projective spectrum
of `A` restricted to the basic open set `D(f)`.

Mathematically, the map is the same as `awayToSection`.
-/
def awayToÎ“ (f) : CommRingCat.of (Aâ°_ f) âŸ¶ LocallyRingedSpace.Î“.obj (op <| Proj| pbo f) :=
  awayToSection ğ’œ f â‰« (ProjectiveSpectrum.Proj.structureSheaf ğ’œ).1.map
    (homOfLE (Opens.isOpenEmbedding_obj_top _).le).op

lemma awayToÎ“_Î“ToStalk (f) (x) :
    awayToÎ“ ğ’œ f â‰« (Proj| pbo f).presheaf.Î“germ x =
      CommRingCat.ofHom (HomogeneousLocalization.mapId ğ’œ (Submonoid.powers_le.mpr x.2)) â‰«
      (Proj.stalkIso' ğ’œ x.1).toCommRingCatIso.inv â‰«
      ((Proj.toLocallyRingedSpace ğ’œ).restrictStalkIso (Opens.isOpenEmbedding _) x).inv := by
  rw [awayToÎ“, Category.assoc, â† Category.assoc _ (Iso.inv _),
    Iso.eq_comp_inv, Category.assoc, Category.assoc, Presheaf.Î“germ]
  rw [LocallyRingedSpace.restrictStalkIso_hom_eq_germ]
  simp only [Proj.toLocallyRingedSpace, Proj.toSheafedSpace]
  rw [Presheaf.germ_res, awayToSection_germ]
  rfl

/--
The morphism of locally ringed space from `Proj|D(f)` to `Spec Aâ°_f` induced by the ring map
`Aâ°_ f â†’ Î“(Proj, D(f))` under the gamma spec adjunction.
-/
def toSpec (f) : (Proj| pbo f) âŸ¶ Spec (Aâ°_ f) :=
  Î“Spec.locallyRingedSpaceAdjunction.homEquiv (Proj| pbo f) (op (CommRingCat.of <| Aâ°_ f))
    (awayToÎ“ ğ’œ f).op

open HomogeneousLocalization IsLocalRing

lemma toSpec_base_apply_eq_comap {f} (x : Proj| pbo f) :
    (toSpec ğ’œ f).base x = PrimeSpectrum.comap (mapId ğ’œ (Submonoid.powers_le.mpr x.2))
      (closedPoint (AtPrime ğ’œ x.1.asHomogeneousIdeal.toIdeal)) := by
  change PrimeSpectrum.comap (awayToÎ“ ğ’œ f â‰« (Proj| pbo f).presheaf.Î“germ x).hom
        (IsLocalRing.closedPoint ((Proj| pbo f).presheaf.stalk x)) = _
  rw [awayToÎ“_Î“ToStalk, CommRingCat.hom_comp, PrimeSpectrum.comap_comp]
  exact congr(PrimeSpectrum.comap _ $(@IsLocalRing.comap_closedPoint
    (HomogeneousLocalization.AtPrime ğ’œ x.1.asHomogeneousIdeal.toIdeal) _ _
    ((Proj| pbo f).presheaf.stalk x) _ _ _ (isLocalHom_of_isIso _)))

lemma toSpec_base_apply_eq {f} (x : Proj| pbo f) :
    (toSpec ğ’œ f).base x = ProjIsoSpecTopComponent.toSpec ğ’œ f x :=
  toSpec_base_apply_eq_comap ğ’œ x |>.trans <| PrimeSpectrum.ext <| Ideal.ext fun z =>
  show Â¬ IsUnit _ â†” z âˆˆ ProjIsoSpecTopComponent.ToSpec.carrier _ by
  obtain âŸ¨z, rflâŸ© := z.mk_surjective
  rw [â† HomogeneousLocalization.isUnit_iff_isUnit_val,
    ProjIsoSpecTopComponent.ToSpec.mk_mem_carrier, HomogeneousLocalization.map_mk,
    HomogeneousLocalization.val_mk, Localization.mk_eq_mk',
    IsLocalization.AtPrime.isUnit_mk'_iff]
  exact not_not

lemma toSpec_base_isIso {f} {m} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) :
    IsIso (toSpec ğ’œ f).base := by
  convert (projIsoSpecTopComponent f_deg hm).isIso_hom
  exact ConcreteCategory.hom_ext _ _ <| toSpec_base_apply_eq ğ’œ

lemma mk_mem_toSpec_base_apply {f} (x : Proj| pbo f)
    (z : NumDenSameDeg ğ’œ (.powers f)) :
    HomogeneousLocalization.mk z âˆˆ ((toSpec ğ’œ f).base x).asIdeal â†”
      z.num.1 âˆˆ x.1.asHomogeneousIdeal :=
  (toSpec_base_apply_eq ğ’œ x).symm â–¸ ProjIsoSpecTopComponent.ToSpec.mk_mem_carrier _ _

lemma toSpec_preimage_basicOpen {f}
    (t : NumDenSameDeg ğ’œ (.powers f)) :
    (Opens.map (toSpec ğ’œ f).base).obj (sbo (HomogeneousLocalization.mk t)) =
      Opens.comap âŸ¨_, continuous_subtype_valâŸ© (pbo t.num.1) :=
  Opens.ext <| Opens.map_coe _ _ â–¸ by
  convert (ProjIsoSpecTopComponent.ToSpec.preimage_basicOpen f t)
  exact funext fun _ => toSpec_base_apply_eq _ _

@[reassoc]
lemma toOpen_toSpec_val_c_app (f) (U) :
    StructureSheaf.toOpen (Aâ°_ f) U.unop â‰« (toSpec ğ’œ f).c.app U =
      awayToÎ“ ğ’œ f â‰« (Proj| pbo f).presheaf.map (homOfLE le_top).op :=
  Eq.trans (by congr) <| Î“Spec.toOpen_comp_locallyRingedSpaceAdjunction_homEquiv_app _ U

@[reassoc]
lemma toStalk_stalkMap_toSpec (f) (x) :
    StructureSheaf.toStalk _ _ â‰« (toSpec ğ’œ f).stalkMap x =
      awayToÎ“ ğ’œ f â‰« (Proj| pbo f).presheaf.Î“germ x := by
  rw [StructureSheaf.toStalk, Category.assoc]
  simp_rw [â† Spec.locallyRingedSpaceObj_presheaf']
  rw [LocallyRingedSpace.stalkMap_germ (toSpec ğ’œ f),
    toOpen_toSpec_val_c_app_assoc, Presheaf.germ_res]
  rfl

/--
If `x` is a point in the basic open set `D(f)` where `f` is a homogeneous element of positive
degree, then the homogeneously localized ring `Aâ°â‚“` has the universal property of the localization
of `Aâ°_f` at `Ï†(x)` where `Ï† : Proj|D(f) âŸ¶ Spec Aâ°_f` is the morphism of locally ringed space
constructed as above.
-/
lemma isLocalization_atPrime (f) (x : pbo f) {m} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) :
    @IsLocalization (Away ğ’œ f) _ ((toSpec ğ’œ f).base x).asIdeal.primeCompl
      (AtPrime ğ’œ x.1.asHomogeneousIdeal.toIdeal) _
      (mapId ğ’œ (Submonoid.powers_le.mpr x.2)).toAlgebra := by
  letI : Algebra (Away ğ’œ f) (AtPrime ğ’œ x.1.asHomogeneousIdeal.toIdeal) :=
    (mapId ğ’œ (Submonoid.powers_le.mpr x.2)).toAlgebra
  constructor
  Â· rintro âŸ¨y, hyâŸ©
    obtain âŸ¨y, rflâŸ© := HomogeneousLocalization.mk_surjective y
    refine isUnit_of_mul_eq_one _
      (.mk âŸ¨y.deg, y.den, y.num, (mk_mem_toSpec_base_apply _ _ _).not.mp hyâŸ©) <| val_injective _ ?_
    simp only [RingHom.algebraMap_toAlgebra, map_mk, RingHom.id_apply, val_mul, val_mk, mk_eq_mk',
      val_one, IsLocalization.mk'_mul_mk'_eq_one']
  Â· intro z
    obtain âŸ¨âŸ¨i, a, âŸ¨b, hbâŸ©, (hb' : b âˆ‰ x.1.1)âŸ©, rflâŸ© := z.mk_surjective
    refine âŸ¨âŸ¨HomogeneousLocalization.mk âŸ¨i * m, âŸ¨a * b ^ (m - 1), ?_âŸ©,
        âŸ¨f ^ i, SetLike.pow_mem_graded _ f_degâŸ©, âŸ¨_, rflâŸ©âŸ©,
      âŸ¨HomogeneousLocalization.mk âŸ¨i * m, âŸ¨b ^ m, mul_comm m i â–¸ SetLike.pow_mem_graded _ hbâŸ©,
        âŸ¨f ^ i, SetLike.pow_mem_graded _ f_degâŸ©, âŸ¨_, rflâŸ©âŸ©,
        (mk_mem_toSpec_base_apply _ _ _).not.mpr <| x.1.1.toIdeal.primeCompl.pow_mem hb' mâŸ©âŸ©,
        val_injective _ ?_âŸ©
    Â· convert SetLike.mul_mem_graded a.2 (SetLike.pow_mem_graded (m - 1) hb) using 2
      rw [â† succ_nsmul', tsub_add_cancel_of_le (by cutsat), mul_comm, smul_eq_mul]
    Â· simp only [RingHom.algebraMap_toAlgebra, map_mk, RingHom.id_apply, val_mul, val_mk,
        mk_eq_mk', â† IsLocalization.mk'_mul, Submonoid.mk_mul_mk, IsLocalization.mk'_eq_iff_eq]
      rw [mul_comm b, mul_mul_mul_comm, â† pow_succ', mul_assoc, tsub_add_cancel_of_le (by cutsat)]
  Â· intro y z e
    obtain âŸ¨y, rflâŸ© := HomogeneousLocalization.mk_surjective y
    obtain âŸ¨z, rflâŸ© := HomogeneousLocalization.mk_surjective z
    obtain âŸ¨i, c, hc, hc', eâŸ© : âˆƒ i, âˆƒ c âˆˆ ğ’œ i, c âˆ‰ x.1.asHomogeneousIdeal âˆ§
        c * (z.den.1 * y.num.1) = c * (y.den.1 * z.num.1) := by
      apply_fun HomogeneousLocalization.val at e
      simp only [RingHom.algebraMap_toAlgebra, map_mk, RingHom.id_apply, val_mk, mk_eq_mk',
        IsLocalization.mk'_eq_iff_eq] at e
      obtain âŸ¨âŸ¨c, hcxâŸ©, hcâŸ© := IsLocalization.exists_of_eq (M := x.1.1.toIdeal.primeCompl) e
      obtain âŸ¨i, hiâŸ© := not_forall.mp ((x.1.1.isHomogeneous.mem_iff _).not.mp hcx)
      refine âŸ¨i, _, (decompose ğ’œ c i).2, hi, ?_âŸ©
      apply_fun fun x â†¦ (decompose ğ’œ x (i + z.deg + y.deg)).1 at hc
      conv_rhs at hc => rw [add_right_comm]
      rwa [â† mul_assoc, coe_decompose_mul_add_of_right_mem, coe_decompose_mul_add_of_right_mem,
        â† mul_assoc, coe_decompose_mul_add_of_right_mem, coe_decompose_mul_add_of_right_mem,
        mul_assoc, mul_assoc] at hc
      exacts [y.den.2, z.num.2, z.den.2, y.num.2]
    refine âŸ¨âŸ¨HomogeneousLocalization.mk âŸ¨m * i, âŸ¨c ^ m, SetLike.pow_mem_graded _ hcâŸ©,
      âŸ¨f ^ i, mul_comm m i â–¸ SetLike.pow_mem_graded _ f_degâŸ©, âŸ¨_, rflâŸ©âŸ©,
      (mk_mem_toSpec_base_apply _ _ _).not.mpr <| x.1.1.toIdeal.primeCompl.pow_mem hc' _âŸ©,
      val_injective _ ?_âŸ©
    simp only [val_mul, val_mk, mk_eq_mk', â† IsLocalization.mk'_mul, Submonoid.mk_mul_mk,
      IsLocalization.mk'_eq_iff_eq, mul_assoc]
    congr 2
    rw [mul_left_comm, mul_left_comm y.den.1, â† tsub_add_cancel_of_le (show 1 â‰¤ m from hm),
      pow_succ, mul_assoc, mul_assoc, e]

/--
For an element `f âˆˆ A` with positive degree and a homogeneous ideal in `D(f)`, we have that the
stalk of `Spec Aâ°_ f` at `y` is isomorphic to `Aâ°â‚“` where `y` is the point in `Proj` corresponding
to `x`.
-/
def specStalkEquiv (f) (x : pbo f) {m} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) :
    (Spec.structureSheaf (Aâ°_ f)).presheaf.stalk ((toSpec ğ’œ f).base x) â‰…
      CommRingCat.of (AtPrime ğ’œ x.1.asHomogeneousIdeal.toIdeal) :=
  letI : Algebra (Away ğ’œ f) (AtPrime ğ’œ x.1.asHomogeneousIdeal.toIdeal) :=
    (mapId ğ’œ (Submonoid.powers_le.mpr x.2)).toAlgebra
  haveI := isLocalization_atPrime ğ’œ f x f_deg hm
  (IsLocalization.algEquiv
    (R := Aâ°_ f)
    (M := ((toSpec ğ’œ f).base x).asIdeal.primeCompl)
    (S := (Spec.structureSheaf (Aâ°_ f)).presheaf.stalk ((toSpec ğ’œ f).base x))
    (Q := AtPrime ğ’œ x.1.asHomogeneousIdeal.toIdeal)).toRingEquiv.toCommRingCatIso

lemma toStalk_specStalkEquiv (f) (x : pbo f) {m} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) :
    StructureSheaf.toStalk (Aâ°_ f) ((toSpec ğ’œ f).base x) â‰« (specStalkEquiv ğ’œ f x f_deg hm).hom =
      CommRingCat.ofHom (mapId _ <| Submonoid.powers_le.mpr x.2) :=
  letI : Algebra (Away ğ’œ f) (AtPrime ğ’œ x.1.asHomogeneousIdeal.toIdeal) :=
    (mapId ğ’œ (Submonoid.powers_le.mpr x.2)).toAlgebra
  letI := isLocalization_atPrime ğ’œ f x f_deg hm
  CommRingCat.hom_ext (IsLocalization.algEquiv
    (R := Aâ°_ f)
    (M := ((toSpec ğ’œ f).base x).asIdeal.primeCompl)
    (S := (Spec.structureSheaf (Aâ°_ f)).presheaf.stalk ((toSpec ğ’œ f).base x))
    (Q := AtPrime ğ’œ x.1.asHomogeneousIdeal.toIdeal)).toAlgHom.comp_algebraMap

lemma stalkMap_toSpec (f) (x : pbo f) {m} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) :
    (toSpec ğ’œ f).stalkMap x =
      (specStalkEquiv ğ’œ f x f_deg hm).hom â‰« (Proj.stalkIso' ğ’œ x.1).toCommRingCatIso.inv â‰«
      ((Proj.toLocallyRingedSpace ğ’œ).restrictStalkIso (Opens.isOpenEmbedding _) x).inv :=
  CommRingCat.hom_ext <|
    IsLocalization.ringHom_ext (R := Aâ°_ f) ((toSpec ğ’œ f).base x).asIdeal.primeCompl
      (S := (Spec.structureSheaf (Aâ°_ f)).presheaf.stalk ((toSpec ğ’œ f).base x)) <|
      CommRingCat.hom_ext_iff.mp <|
        (toStalk_stalkMap_toSpec _ _ _).trans <| by
        rw [awayToÎ“_Î“ToStalk, â† toStalk_specStalkEquiv, Category.assoc]; rfl

lemma isIso_toSpec (f) {m} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) :
    IsIso (toSpec ğ’œ f) := by
  haveI : IsIso (toSpec ğ’œ f).base := toSpec_base_isIso ğ’œ f_deg hm
  haveI _ (x) : IsIso ((toSpec ğ’œ f).stalkMap x) := by
    rw [stalkMap_toSpec ğ’œ f x f_deg hm]; infer_instance
  haveI : LocallyRingedSpace.IsOpenImmersion (toSpec ğ’œ f) :=
    LocallyRingedSpace.IsOpenImmersion.of_stalk_iso (toSpec ğ’œ f)
      (TopCat.homeoOfIso (asIso <| (toSpec ğ’œ f).base)).isOpenEmbedding
  exact LocallyRingedSpace.IsOpenImmersion.to_iso _

end ProjectiveSpectrum.Proj

open ProjectiveSpectrum.Proj in
/--
If `f âˆˆ A` is a homogeneous element of positive degree, then the projective spectrum restricted to
`D(f)` as a locally ringed space is isomorphic to `Spec Aâ°_f`.
-/
def projIsoSpec (f) {m} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m) :
    (Proj| pbo f) â‰… (Spec (Aâ°_ f)) :=
  @asIso _ _ _ _ (f := toSpec ğ’œ f) (isIso_toSpec ğ’œ f f_deg hm)

/--
This is the scheme `Proj(A)` for any `â„•`-graded ring `A`.
-/
def Â«ProjÂ» : Scheme where
  __ := Proj.toLocallyRingedSpace ğ’œ
  local_affine (x : Proj.T) := by
    classical
    obtain âŸ¨f, m, f_deg, hm, hxâŸ© : âˆƒ (f : A) (m : â„•) (_ : f âˆˆ ğ’œ m) (_ : 0 < m), f âˆ‰ x.1 := by
      by_contra!
      refine x.not_irrelevant_le fun z hz â†¦ ?_
      rw [â† DirectSum.sum_support_decompose ğ’œ z]
      exact x.1.toIdeal.sum_mem fun k hk â†¦ this _ k (SetLike.coe_mem _) <| by_contra <| by aesop
    exact âŸ¨âŸ¨pbo f, hxâŸ©, .of (Aâ°_ f), âŸ¨projIsoSpec ğ’œ f f_deg hmâŸ©âŸ©


end AlgebraicGeometry
