/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Andrew Yang
-/
import Mathlib.AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf
import Mathlib.AlgebraicGeometry.GammaSpecAdjunction
import Mathlib.RingTheory.GradedAlgebra.Radical

/-!
# Proj as a scheme

This file is to prove that `Proj` is a scheme.

## Notation

* `Proj`      : `Proj` as a locally ringed space
* `Proj.T`    : the underlying topological space of `Proj`
* `Proj| U`   : `Proj` restricted to some open set `U`
* `Proj.T| U` : the underlying topological space of `Proj` restricted to open set `U`
* `pbo f`     : basic open set at `f` in `Proj`
* `Spec`      : `Spec` as a locally ringed space
* `Spec.T`    : the underlying topological space of `Spec`
* `sbo g`     : basic open set at `g` in `Spec`
* `A‚Å∞_x`      : the degree zero part of localized ring `A‚Çì`

## Implementation

In `AlgebraicGeometry/ProjectiveSpectrum/StructureSheaf.lean`, we have given `Proj` a
structure sheaf so that `Proj` is a locally ringed space. In this file we will prove that `Proj`
equipped with this structure sheaf is a scheme. We achieve this by using an affine cover by basic
open sets in `Proj`, more specifically:

1. We prove that `Proj` can be covered by basic open sets at homogeneous element of positive degree.
2. We prove that for any homogeneous element `f : A` of positive degree `m`, `Proj.T | (pbo f)` is
    homeomorphic to `Spec.T A‚Å∞_f`:
  - forward direction `toSpec`:
    for any `x : pbo f`, i.e. a relevant homogeneous prime ideal `x`, send it to
    `A‚Å∞_f ‚à© span {g / 1 | g ‚àà x}` (see `ProjIsoSpecTopComponent.IoSpec.carrier`). This ideal is
    prime, the proof is in `ProjIsoSpecTopComponent.ToSpec.toFun`. The fact that this function
    is continuous is found in `ProjIsoSpecTopComponent.toSpec`
  - backward direction `fromSpec`:
    for any `q : Spec A‚Å∞_f`, we send it to `{a | ‚àÄ i, a·µ¢·µê/f‚Å± ‚àà q}`; we need this to be a
    homogeneous prime ideal that is relevant.
    * This is in fact an ideal, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal`;
    * This ideal is also homogeneous, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal.homogeneous`;
    * This ideal is relevant, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.relevant`;
    * This ideal is prime, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal.prime`.
    Hence we have a well defined function `Spec.T A‚Å∞_f ‚Üí Proj.T | (pbo f)`, this function is called
    `ProjIsoSpecTopComponent.FromSpec.toFun`. But to prove the continuity of this function, we need
    to prove `fromSpec ‚àò toSpec` and `toSpec ‚àò fromSpec` are both identities; these are achieved in
    `ProjIsoSpecTopComponent.fromSpec_toSpec` and `ProjIsoSpecTopComponent.toSpec_fromSpec`.
3. Then we construct a morphism of locally ringed spaces `Œ± : Proj| (pbo f) ‚ü∂ Spec.T A‚Å∞_f` as the
    following: by the Gamma-Spec adjunction, it is sufficient to construct a ring map
    `A‚Å∞_f ‚Üí Œì(Proj, pbo f)` from the ring of homogeneous localization of `A` away from `f` to the
    local sections of structure sheaf of projective spectrum on the basic open set around `f`.
    The map `A‚Å∞_f ‚Üí Œì(Proj, pbo f)` is constructed in `awayToŒì` and is defined by sending
    `s ‚àà A‚Å∞_f` to the section `x ‚Ü¶ s` on `pbo f`.

## Main Definitions and Statements

For a homogeneous element `f` of degree `m`
* `ProjIsoSpecTopComponent.toSpec`: the continuous map between `Proj.T| pbo f` and `Spec.T A‚Å∞_f`
  defined by sending `x : Proj| (pbo f)` to `A‚Å∞_f ‚à© span {g / 1 | g ‚àà x}`. We also denote this map
  as `œà`.
* `ProjIsoSpecTopComponent.ToSpec.preimage_eq`: for any `a: A`, if `a/f^m` has degree zero,
  then the preimage of `sbo a/f^m` under `toSpec f` is `pbo f ‚à© pbo a`.

If we further assume `m` is positive
* `ProjIsoSpecTopComponent.fromSpec`: the continuous map between `Spec.T A‚Å∞_f` and `Proj.T| pbo f`
  defined by sending `q` to `{a | a·µ¢·µê/f‚Å± ‚àà q}` where `a·µ¢` is the `i`-th coordinate of `a`.
  We also denote this map as `œÜ`
* `projIsoSpecTopComponent`: the homeomorphism `Proj.T| pbo f ‚âÖ Spec.T A‚Å∞_f` obtained by `œÜ` and
  `œà`.
* `ProjectiveSpectrum.Proj.toSpec`: the morphism of locally ringed spaces between `Proj| pbo f`
  and `Spec A‚Å∞_f` corresponding to the ring map `A‚Å∞_f ‚Üí Œì(Proj, pbo f)` under the Gamma-Spec
  adjunction defined by sending `s` to the section `x ‚Ü¶ s` on `pbo f`.

Finally,
* `AlgebraicGeometry.Proj`: for any `‚Ñï`-graded ring `A`, `Proj A` is locally affine, hence is a
  scheme.

## Reference
* [Robin Hartshorne, *Algebraic Geometry*][Har77]: Chapter II.2 Proposition 2.5
-/

noncomputable section


namespace AlgebraicGeometry

open scoped DirectSum Pointwise

open DirectSum SetLike.GradedMonoid Localization

open Finset hiding mk_zero

variable {R A : Type*}
variable [CommRing R] [CommRing A] [Algebra R A]
variable (ùíú : ‚Ñï ‚Üí Submodule R A)
variable [GradedAlgebra ùíú]

open TopCat TopologicalSpace

open CategoryTheory Opposite

open ProjectiveSpectrum.StructureSheaf

-- Porting note: currently require lack of hygiene to use in variable declarations
-- maybe all make into notation3?
set_option hygiene false
/-- `Proj` as a locally ringed space -/
local notation3 "Proj" => Proj.toLocallyRingedSpace ùíú

/-- The underlying topological space of `Proj` -/
local notation3 "Proj.T" => PresheafedSpace.carrier <| SheafedSpace.toPresheafedSpace
  <| LocallyRingedSpace.toSheafedSpace <| Proj.toLocallyRingedSpace ùíú

/-- `Proj` restrict to some open set -/
macro "Proj| " U:term : term =>
  `((Proj.toLocallyRingedSpace ùíú).restrict (Opens.openEmbedding (X := Proj.T) ($U : Opens Proj.T)))

/-- the underlying topological space of `Proj` restricted to some open set -/
local notation "Proj.T| " U => PresheafedSpace.carrier <| SheafedSpace.toPresheafedSpace
  <| LocallyRingedSpace.toSheafedSpace
    <| (LocallyRingedSpace.restrict Proj (Opens.openEmbedding (X := Proj.T) (U : Opens Proj.T)))

/-- basic open sets in `Proj` -/
local notation "pbo " x => ProjectiveSpectrum.basicOpen ùíú x

/-- basic open sets in `Spec` -/
local notation "sbo " f => PrimeSpectrum.basicOpen f

/-- `Spec` as a locally ringed space -/
local notation3 "Spec " ring => Spec.locallyRingedSpaceObj (CommRingCat.of ring)

/-- the underlying topological space of `Spec` -/
local notation "Spec.T " ring =>
  (Spec.locallyRingedSpaceObj (CommRingCat.of ring)).toSheafedSpace.toPresheafedSpace.1

local notation3 "A‚Å∞_ " f => HomogeneousLocalization.Away ùíú f

namespace ProjIsoSpecTopComponent

/-
This section is to construct the homeomorphism between `Proj` restricted at basic open set at
a homogeneous element `x` and `Spec A‚Å∞‚Çì` where `A‚Å∞‚Çì` is the degree zero part of the localized
ring `A‚Çì`.
-/
namespace ToSpec

open Ideal

-- This section is to construct the forward direction :
-- So for any `x` in `Proj| (pbo f)`, we need some point in `Spec A‚Å∞_f`, i.e. a prime ideal,
-- and we need this correspondence to be continuous in their Zariski topology.
variable {ùíú}
variable {f : A} {m : ‚Ñï} (x : Proj| (pbo f))

/--
For any `x` in `Proj| (pbo f)`, the corresponding ideal in `Spec A‚Å∞_f`. This fact that this ideal
is prime is proven in `TopComponent.Forward.toFun`-/
def carrier : Ideal (A‚Å∞_ f) :=
  Ideal.comap (algebraMap (A‚Å∞_ f) (Away f))
    (x.val.asHomogeneousIdeal.toIdeal.map (algebraMap A (Away f)))

@[simp]
theorem mk_mem_carrier (z : HomogeneousLocalization.NumDenSameDeg ùíú (.powers f)) :
    HomogeneousLocalization.mk z ‚àà carrier x ‚Üî z.num.1 ‚àà x.1.asHomogeneousIdeal := by
  rw [carrier, Ideal.mem_comap, HomogeneousLocalization.algebraMap_apply,
    HomogeneousLocalization.val_mk, Localization.mk_eq_mk', IsLocalization.mk'_eq_mul_mk'_one,
    mul_comm, Ideal.unit_mul_mem_iff_mem, ‚Üê Ideal.mem_comap,
    IsLocalization.comap_map_of_isPrime_disjoint (.powers f)]
  ¬∑ rfl
  ¬∑ infer_instance
  ¬∑ exact (disjoint_powers_iff_not_mem _ (Ideal.IsPrime.isRadical inferInstance)).mpr x.2
  ¬∑ exact isUnit_of_invertible _

theorem isPrime_carrier : Ideal.IsPrime (carrier x) := by
  refine Ideal.IsPrime.comap _ (hK := ?_)
  exact IsLocalization.isPrime_of_isPrime_disjoint
    (Submonoid.powers f) _ _ inferInstance
    ((disjoint_powers_iff_not_mem _ (Ideal.IsPrime.isRadical inferInstance)).mpr x.2)

variable (f)

/-- The function between the basic open set `D(f)` in `Proj` to the corresponding basic open set in
`Spec A‚Å∞_f`. This is bundled into a continuous map in `TopComponent.forward`.
-/
@[simps (config := .lemmasOnly)]
def toFun (x : Proj.T| pbo f) : Spec.T A‚Å∞_ f :=
  ‚ü®carrier x, isPrime_carrier x‚ü©

/-
The preimage of basic open set `D(a/f^n)` in `Spec A‚Å∞_f` under the forward map from `Proj A` to
`Spec A‚Å∞_f` is the basic open set `D(a) ‚à© D(f)` in `Proj A`. This lemma is used to prove that the
forward map is continuous.
-/
theorem preimage_basicOpen (z : HomogeneousLocalization.NumDenSameDeg ùíú (.powers f)) :
    toFun f ‚Åª¬π' (sbo (HomogeneousLocalization.mk z) : Set (PrimeSpectrum (A‚Å∞_ f))) =
      Subtype.val ‚Åª¬π' (pbo z.num.1 : Set (ProjectiveSpectrum ùíú)) :=
  Set.ext fun y ‚Ü¶ (mk_mem_carrier y z).not

end ToSpec

section

/-- The continuous function from the basic open set `D(f)` in `Proj`
to the corresponding basic open set in `Spec A‚Å∞_f`. -/
@[simps! (config := .lemmasOnly) apply_asIdeal]
def toSpec (f : A) : (Proj.T| pbo f) ‚ü∂ Spec.T A‚Å∞_ f where
  toFun := ToSpec.toFun f
  continuous_toFun := by
    rw [PrimeSpectrum.isTopologicalBasis_basic_opens.continuous_iff]
    rintro _ ‚ü®x, rfl‚ü©
    obtain ‚ü®x, rfl‚ü© := Quotient.surjective_Quotient_mk'' x
    rw [ToSpec.preimage_basicOpen]
    exact (pbo x.num).2.preimage continuous_subtype_val

variable {ùíú} in
lemma toSpec_preimage_basicOpen {f} (z : HomogeneousLocalization.NumDenSameDeg ùíú (.powers f)) :
    toSpec ùíú f ‚Åª¬π' (sbo (HomogeneousLocalization.mk z) : Set (PrimeSpectrum (A‚Å∞_ f))) =
      Subtype.val ‚Åª¬π' (pbo z.num.1 : Set (ProjectiveSpectrum ùíú)) :=
  ToSpec.preimage_basicOpen f z

end

namespace FromSpec

open GradedAlgebra SetLike

open Finset hiding mk_zero

-- Porting note: _root_ doesn't work here
open HomogeneousLocalization

variable {ùíú}
variable {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m)

open Lean Meta Elab Tactic

macro "mem_tac_aux" : tactic =>
  `(tactic| first | exact pow_mem_graded _ (Submodule.coe_mem _) | exact natCast_mem_graded _ _ |
    exact pow_mem_graded _ f_deg)

macro "mem_tac" : tactic =>
  `(tactic| first | mem_tac_aux |
    repeat (all_goals (apply SetLike.GradedMonoid.toGradedMul.mul_mem)); mem_tac_aux)

/-- The function from `Spec A‚Å∞_f` to `Proj|D(f)` is defined by `q ‚Ü¶ {a | a·µ¢·µê/f‚Å± ‚àà q}`, i.e. sending
`q` a prime ideal in `A‚Å∞_f` to the homogeneous prime relevant ideal containing only and all the
elements `a : A` such that for every `i`, the degree 0 element formed by dividing the `m`-th power
of the `i`-th projection of `a` by the `i`-th power of the degree-`m` homogeneous element `f`,
lies in `q`.

The set `{a | a·µ¢·µê/f‚Å± ‚àà q}`
* is an ideal, as proved in `carrier.asIdeal`;
* is homogeneous, as proved in `carrier.asHomogeneousIdeal`;
* is prime, as proved in `carrier.asIdeal.prime`;
* is relevant, as proved in `carrier.relevant`.
-/
def carrier (f_deg : f ‚àà ùíú m) (q : Spec.T A‚Å∞_ f) : Set A :=
  {a | ‚àÄ i, (HomogeneousLocalization.mk ‚ü®m * i, ‚ü®proj ùíú i a ^ m, by rw [‚Üê smul_eq_mul]; mem_tac‚ü©,
              ‚ü®f ^ i, by rw [mul_comm]; mem_tac‚ü©, ‚ü®_, rfl‚ü©‚ü© : A‚Å∞_ f) ‚àà q.1}

theorem mem_carrier_iff (q : Spec.T A‚Å∞_ f) (a : A) :
    a ‚àà carrier f_deg q ‚Üî ‚àÄ i, (HomogeneousLocalization.mk ‚ü®m * i, ‚ü®proj ùíú i a ^ m, by
      rw [‚Üê smul_eq_mul]; mem_tac‚ü©,
      ‚ü®f ^ i, by rw [mul_comm]; mem_tac‚ü©, ‚ü®_, rfl‚ü©‚ü© : A‚Å∞_ f) ‚àà q.1 :=
  Iff.rfl

theorem mem_carrier_iff' (q : Spec.T A‚Å∞_ f) (a : A) :
    a ‚àà carrier f_deg q ‚Üî
      ‚àÄ i, (Localization.mk (proj ùíú i a ^ m) ‚ü®f ^ i, ‚ü®i, rfl‚ü©‚ü© : Localization.Away f) ‚àà
          algebraMap (HomogeneousLocalization.Away ùíú f) (Localization.Away f) '' { s | s ‚àà q.1 } :=
  (mem_carrier_iff f_deg q a).trans
    (by
      constructor <;> intro h i <;> specialize h i
      ¬∑ rw [Set.mem_image]; refine ‚ü®_, h, rfl‚ü©
      ¬∑ rw [Set.mem_image] at h; rcases h with ‚ü®x, h, hx‚ü©
        change x ‚àà q.asIdeal at h
        convert h
        rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk]
        dsimp only [Subtype.coe_mk]; rw [‚Üê hx]; rfl)

theorem mem_carrier_iff_of_mem (hm : 0 < m) (q : Spec.T A‚Å∞_ f) (a : A) {n} (hn : a ‚àà ùíú n) :
    a ‚àà carrier f_deg q ‚Üî
      (HomogeneousLocalization.mk ‚ü®m * n, ‚ü®a ^ m, pow_mem_graded m hn‚ü©,
        ‚ü®f ^ n, by rw [mul_comm]; mem_tac‚ü©, ‚ü®_, rfl‚ü©‚ü© : A‚Å∞_ f) ‚àà q.asIdeal := by
  trans (HomogeneousLocalization.mk ‚ü®m * n, ‚ü®proj ùíú n a ^ m, by rw [‚Üê smul_eq_mul]; mem_tac‚ü©,
    ‚ü®f ^ n, by rw [mul_comm]; mem_tac‚ü©, ‚ü®_, rfl‚ü©‚ü© : A‚Å∞_ f) ‚àà q.asIdeal
  ¬∑ refine ‚ü®fun h ‚Ü¶ h n, fun h i ‚Ü¶ if hi : i = n then hi ‚ñ∏ h else ?_‚ü©
    convert zero_mem q.asIdeal
    apply HomogeneousLocalization.val_injective
    simp only [proj_apply, decompose_of_mem_ne _ hn (Ne.symm hi), zero_pow hm.ne',
      HomogeneousLocalization.val_mk, Localization.mk_zero, HomogeneousLocalization.val_zero]
  ¬∑ simp only [proj_apply, decompose_of_mem_same _ hn]

theorem mem_carrier_iff_of_mem_mul (hm : 0 < m)
    (q : Spec.T A‚Å∞_ f) (a : A) {n} (hn : a ‚àà ùíú (n * m)) :
    a ‚àà carrier f_deg q ‚Üî (HomogeneousLocalization.mk ‚ü®m * n, ‚ü®a, mul_comm n m ‚ñ∏ hn‚ü©,
        ‚ü®f ^ n, by rw [mul_comm]; mem_tac‚ü©, ‚ü®_, rfl‚ü©‚ü© : A‚Å∞_ f) ‚àà q.asIdeal := by
  rw [mem_carrier_iff_of_mem f_deg hm q a hn, iff_iff_eq, eq_comm,
    ‚Üê Ideal.IsPrime.pow_mem_iff_mem (Œ± := A‚Å∞_ f) inferInstance m hm]
  congr 1
  apply HomogeneousLocalization.val_injective
  simp only [HomogeneousLocalization.val_mk, HomogeneousLocalization.val_pow,
    Localization.mk_pow, pow_mul]
  rfl

theorem num_mem_carrier_iff (hm : 0 < m) (q : Spec.T A‚Å∞_ f)
    (z : HomogeneousLocalization.NumDenSameDeg ùíú (.powers f)) :
    z.num.1 ‚àà carrier f_deg q ‚Üî HomogeneousLocalization.mk z ‚àà q.asIdeal := by
  obtain ‚ü®n, hn : f ^ n = _‚ü© := z.den_mem
  have : f ^ n ‚â† 0 := fun e ‚Ü¶ by
    have := HomogeneousLocalization.subsingleton ùíú (x := .powers f) ‚ü®n, e‚ü©
    exact IsEmpty.elim (inferInstanceAs (IsEmpty (PrimeSpectrum (A‚Å∞_ f)))) q
  convert mem_carrier_iff_of_mem_mul f_deg hm q z.num.1 (n := n) ?_ using 2
  ¬∑ apply HomogeneousLocalization.val_injective; simp only [hn, HomogeneousLocalization.val_mk]
  ¬∑ have := degree_eq_of_mem_mem ùíú (SetLike.pow_mem_graded n f_deg) (hn.symm ‚ñ∏ z.den.2) this
    rw [‚Üê smul_eq_mul, this]; exact z.num.2

theorem carrier.add_mem (q : Spec.T A‚Å∞_ f) {a b : A} (ha : a ‚àà carrier f_deg q)
    (hb : b ‚àà carrier f_deg q) : a + b ‚àà carrier f_deg q := by
  refine fun i => (q.2.mem_or_mem ?_).elim id id
  change (.mk ‚ü®_, _, _, _‚ü© : A‚Å∞_ f) ‚àà q.1; dsimp only [Subtype.coe_mk]
  simp_rw [‚Üê pow_add, map_add, add_pow, mul_comm, ‚Üê nsmul_eq_mul]
  let g : ‚Ñï ‚Üí A‚Å∞_ f := fun j => (m + m).choose j ‚Ä¢
      if h2 : m + m < j then (0 : A‚Å∞_ f)
      else
        -- Porting note: inlining `l`, `r` causes a "can't synth HMul A‚Å∞_ f A‚Å∞_ f ?" error
        if h1 : j ‚â§ m then
          letI l : A‚Å∞_ f := HomogeneousLocalization.mk
            ‚ü®m * i, ‚ü®proj ùíú i a ^ j * proj ùíú i b ^ (m - j), ?_‚ü©,
              ‚ü®_, by rw [mul_comm]; mem_tac‚ü©, ‚ü®i, rfl‚ü©‚ü©
          letI r : A‚Å∞_ f := HomogeneousLocalization.mk
            ‚ü®m * i, ‚ü®proj ùíú i b ^ m, by rw [‚Üê smul_eq_mul]; mem_tac‚ü©,
              ‚ü®_, by rw [mul_comm]; mem_tac‚ü©, ‚ü®i, rfl‚ü©‚ü©
          l * r
        else
          letI l : A‚Å∞_ f := HomogeneousLocalization.mk
            ‚ü®m * i, ‚ü®proj ùíú i a ^ m, by rw [‚Üê smul_eq_mul]; mem_tac‚ü©,
              ‚ü®_, by rw [mul_comm]; mem_tac‚ü©, ‚ü®i, rfl‚ü©‚ü©
          letI r : A‚Å∞_ f := HomogeneousLocalization.mk
            ‚ü®m * i, ‚ü®proj ùíú i a ^ (j - m) * proj ùíú i b ^ (m + m - j), ?_‚ü©,
              ‚ü®_, by rw [mul_comm]; mem_tac‚ü©, ‚ü®i, rfl‚ü©‚ü©
          l * r
  rotate_left
  ¬∑ rw [(_ : m * i = _)]
    -- Porting note: it seems unification with mul_mem is more fiddly reducing value of mem_tac
    apply GradedMonoid.toGradedMul.mul_mem (i := j ‚Ä¢ i) (j := (m - j) ‚Ä¢ i) <;> mem_tac_aux
    rw [‚Üê add_smul, Nat.add_sub_of_le h1]; rfl
  ¬∑ rw [(_ : m * i = _)]
    apply GradedMonoid.toGradedMul.mul_mem (i := (j-m) ‚Ä¢ i) (j := (m + m - j) ‚Ä¢ i) <;> mem_tac_aux
    rw [‚Üê add_smul]; congr; zify [le_of_not_lt h2, le_of_not_le h1]; abel
  convert_to ‚àë i ‚àà range (m + m + 1), g i ‚àà q.1; swap
  ¬∑ refine q.1.sum_mem fun j _ => nsmul_mem ?_ _; split_ifs
    exacts [q.1.zero_mem, q.1.mul_mem_left _ (hb i), q.1.mul_mem_right _ (ha i)]
  rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk]
  change _ = (algebraMap (HomogeneousLocalization.Away ùíú f) (Localization.Away f)) _
  dsimp only [Subtype.coe_mk]; rw [map_sum, mk_sum]
  apply Finset.sum_congr rfl fun j hj => _
  intro j hj
  change _ = HomogeneousLocalization.val _
  rw [HomogeneousLocalization.val_smul]
  split_ifs with h2 h1
  ¬∑ exact ((Finset.mem_range.1 hj).not_le h2).elim
  all_goals simp only [HomogeneousLocalization.val_mul, HomogeneousLocalization.val_zero,
    HomogeneousLocalization.val_mk, Subtype.coe_mk, Localization.mk_mul, ‚Üê smul_mk]; congr 2
  ¬∑ dsimp; rw [mul_assoc, ‚Üê pow_add, add_comm (m - j), Nat.add_sub_assoc h1]
  ¬∑ simp_rw [pow_add]; rfl
  ¬∑ dsimp; rw [‚Üê mul_assoc, ‚Üê pow_add, Nat.add_sub_of_le (le_of_not_le h1)]
  ¬∑ simp_rw [pow_add]; rfl

variable (hm : 0 < m) (q : Spec.T A‚Å∞_ f)
include hm

theorem carrier.zero_mem : (0 : A) ‚àà carrier f_deg q := fun i => by
  convert Submodule.zero_mem q.1 using 1
  rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk,
    HomogeneousLocalization.val_zero]; simp_rw [map_zero, zero_pow hm.ne']
  convert Localization.mk_zero (S := Submonoid.powers f) _ using 1

theorem carrier.smul_mem (c x : A) (hx : x ‚àà carrier f_deg q) : c ‚Ä¢ x ‚àà carrier f_deg q := by
  revert c
  refine DirectSum.Decomposition.inductionOn ùíú ?_ ?_ ?_
  ¬∑ rw [zero_smul]; exact carrier.zero_mem f_deg hm _
  ¬∑ rintro n ‚ü®a, ha‚ü© i
    simp_rw [proj_apply, smul_eq_mul, coe_decompose_mul_of_left_mem ùíú i ha]
    -- Porting note: having trouble with Mul instance
    let product : A‚Å∞_ f :=
      Mul.mul (HomogeneousLocalization.mk ‚ü®_, ‚ü®a ^ m, pow_mem_graded m ha‚ü©, ‚ü®_, ?_‚ü©, ‚ü®n, rfl‚ü©‚ü©)
        (HomogeneousLocalization.mk ‚ü®_, ‚ü®proj ùíú (i - n) x ^ m, by mem_tac‚ü©, ‚ü®_, ?_‚ü©, ‚ü®i - n, rfl‚ü©‚ü©)
    ¬∑ split_ifs with h
      ¬∑ convert_to product ‚àà q.1
        ¬∑ dsimp [product]
          erw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk,
            HomogeneousLocalization.val_mul, HomogeneousLocalization.val_mk,
            HomogeneousLocalization.val_mk]
          ¬∑ simp_rw [mul_pow]; rw [Localization.mk_mul]
            ¬∑ congr; erw [‚Üê pow_add, Nat.add_sub_of_le h]
        ¬∑ apply Ideal.mul_mem_left (Œ± := A‚Å∞_ f) _ _ (hx _)
          rw [(_ : m ‚Ä¢ n = _)]
          ¬∑ mem_tac
          ¬∑ simp only [smul_eq_mul, mul_comm]
      ¬∑ simpa only [map_zero, zero_pow hm.ne'] using zero_mem f_deg hm q i
    rw [(_ : m ‚Ä¢ (i - n) = _)]
    ¬∑ mem_tac
    ¬∑ simp only [smul_eq_mul, mul_comm]
  ¬∑ simp_rw [add_smul]; exact fun _ _ => carrier.add_mem f_deg q

/-- For a prime ideal `q` in `A‚Å∞_f`, the set `{a | a·µ¢·µê/f‚Å± ‚àà q}` as an ideal.
-/
def carrier.asIdeal : Ideal A where
  carrier := carrier f_deg q
  zero_mem' := carrier.zero_mem f_deg hm q
  add_mem' := carrier.add_mem f_deg q
  smul_mem' := carrier.smul_mem f_deg hm q


theorem carrier.asIdeal.homogeneous : (carrier.asIdeal f_deg hm q).IsHomogeneous ùíú :=
  fun i a ha j =>
  (em (i = j)).elim (fun h => h ‚ñ∏ by simpa only [proj_apply, decompose_coe, of_eq_same] using ha _)
    fun h => by
    simpa only [proj_apply, decompose_of_mem_ne ùíú (Submodule.coe_mem (decompose ùíú a i)) h,
      zero_pow hm.ne', map_zero] using carrier.zero_mem f_deg hm q j

/-- For a prime ideal `q` in `A‚Å∞_f`, the set `{a | a·µ¢·µê/f‚Å± ‚àà q}` as a homogeneous ideal.
-/
def carrier.asHomogeneousIdeal : HomogeneousIdeal ùíú :=
  ‚ü®carrier.asIdeal f_deg hm q, carrier.asIdeal.homogeneous f_deg hm q‚ü©

theorem carrier.denom_not_mem : f ‚àâ carrier.asIdeal f_deg hm q := fun rid =>
  q.isPrime.ne_top <|
    (Ideal.eq_top_iff_one _).mpr
      (by
        convert rid m
        rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_one,
          HomogeneousLocalization.val_mk]
        dsimp
        simp_rw [decompose_of_mem_same _ f_deg]
        simp only [mk_eq_monoidOf_mk', Submonoid.LocalizationMap.mk'_self])

theorem carrier.relevant : ¬¨HomogeneousIdeal.irrelevant ùíú ‚â§ carrier.asHomogeneousIdeal f_deg hm q :=
  fun rid => carrier.denom_not_mem f_deg hm q <| rid <| DirectSum.decompose_of_mem_ne ùíú f_deg hm.ne'

theorem carrier.asIdeal.ne_top : carrier.asIdeal f_deg hm q ‚â† ‚ä§ := fun rid =>
  carrier.denom_not_mem f_deg hm q (rid.symm ‚ñ∏ Submodule.mem_top)

theorem carrier.asIdeal.prime : (carrier.asIdeal f_deg hm q).IsPrime :=
  (carrier.asIdeal.homogeneous f_deg hm q).isPrime_of_homogeneous_mem_or_mem
    (carrier.asIdeal.ne_top f_deg hm q) fun {x y} ‚ü®nx, hnx‚ü© ‚ü®ny, hny‚ü© hxy =>
    show (‚àÄ i, _ ‚àà _) ‚à® ‚àÄ i, _ ‚àà _ by
      rw [‚Üê and_forall_ne nx, and_iff_left, ‚Üê and_forall_ne ny, and_iff_left]
      ¬∑ apply q.2.mem_or_mem; convert hxy (nx + ny) using 1
        dsimp
        simp_rw [decompose_of_mem_same ùíú hnx, decompose_of_mem_same ùíú hny,
          decompose_of_mem_same ùíú (SetLike.GradedMonoid.toGradedMul.mul_mem hnx hny),
          mul_pow, pow_add]
        simp only [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk,
          HomogeneousLocalization.val_mul, Localization.mk_mul]
        simp only [Submonoid.mk_mul_mk, mk_eq_monoidOf_mk']
      all_goals
        intro n hn; convert q.1.zero_mem using 1
        rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk,
          HomogeneousLocalization.val_zero]; simp_rw [proj_apply]
        convert mk_zero (S := Submonoid.powers f) _
        rw [decompose_of_mem_ne ùíú _ hn.symm, zero_pow hm.ne']
        ¬∑ first | exact hnx | exact hny

/-- The function `Spec A‚Å∞_f ‚Üí Proj|D(f)` sending `q` to `{a | a·µ¢·µê/f‚Å± ‚àà q}`. -/
def toFun : (Spec.T A‚Å∞_ f) ‚Üí Proj.T| pbo f := fun q =>
  ‚ü®‚ü®carrier.asHomogeneousIdeal f_deg hm q, carrier.asIdeal.prime f_deg hm q,
      carrier.relevant f_deg hm q‚ü©,
    (ProjectiveSpectrum.mem_basicOpen _ f _).mp <| carrier.denom_not_mem f_deg hm q‚ü©

end FromSpec

section toSpecFromSpec

lemma toSpec_fromSpec {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m) (hm : 0 < m) (x : Spec.T (A‚Å∞_ f)) :
    toSpec ùíú f (FromSpec.toFun f_deg hm x) = x := by
  apply PrimeSpectrum.ext
  ext z
  obtain ‚ü®z, rfl‚ü© := z.mk_surjective
  rw [‚Üê FromSpec.num_mem_carrier_iff f_deg hm x]
  exact ToSpec.mk_mem_carrier _ z

@[deprecated (since := "2024-03-02")] alias toSpecFromSpec := toSpec_fromSpec

end toSpecFromSpec

section fromSpecToSpec

lemma fromSpec_toSpec {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m) (hm : 0 < m) (x : Proj.T| pbo f) :
    FromSpec.toFun f_deg hm (toSpec ùíú f x) = x := by
  refine Subtype.ext <| ProjectiveSpectrum.ext <| HomogeneousIdeal.ext' ?_
  intros i z hzi
  refine (FromSpec.mem_carrier_iff_of_mem f_deg hm _ _ hzi).trans ?_
  exact (ToSpec.mk_mem_carrier _ _).trans (x.1.2.pow_mem_iff_mem m hm)

lemma toSpec_injective {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m) (hm : 0 < m) :
    Function.Injective (toSpec ùíú f) := by
  intro x‚ÇÅ x‚ÇÇ h
  have := congr_arg (FromSpec.toFun f_deg hm) h
  rwa [fromSpec_toSpec, fromSpec_toSpec] at this

lemma toSpec_surjective {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m) (hm : 0 < m) :
    Function.Surjective (toSpec ùíú f) :=
  Function.surjective_iff_hasRightInverse |>.mpr
    ‚ü®FromSpec.toFun f_deg hm, toSpec_fromSpec ùíú f_deg hm‚ü©

lemma toSpec_bijective {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m) (hm : 0 < m) :
    Function.Bijective (toSpec (ùíú := ùíú) (f := f)) :=
  ‚ü®toSpec_injective ùíú f_deg hm, toSpec_surjective ùíú f_deg hm‚ü©

end fromSpecToSpec

namespace toSpec

variable {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m) (hm : 0 < m)
include hm f_deg

variable {ùíú} in
lemma image_basicOpen_eq_basicOpen (a : A) (i : ‚Ñï) :
    toSpec ùíú f '' (Subtype.val ‚Åª¬π' (pbo (decompose ùíú a i) : Set (ProjectiveSpectrum ùíú))) =
    (PrimeSpectrum.basicOpen (R := A‚Å∞_ f) <|
      HomogeneousLocalization.mk
        ‚ü®m * i, ‚ü®decompose ùíú a i ^ m,
          (smul_eq_mul ‚Ñï) ‚ñ∏ SetLike.pow_mem_graded _ (Submodule.coe_mem _)‚ü©,
          ‚ü®f^i, by rw [mul_comm]; exact SetLike.pow_mem_graded _ f_deg‚ü©, ‚ü®i, rfl‚ü©‚ü©).1 :=
  Set.preimage_injective.mpr (toSpec_surjective ùíú f_deg hm) <|
    Set.preimage_image_eq _ (toSpec_injective ùíú f_deg hm) ‚ñ∏ by
  rw [Opens.carrier_eq_coe, toSpec_preimage_basicOpen, ProjectiveSpectrum.basicOpen_pow ùíú _ m hm]

end toSpec

variable {ùíú} in
/-- The continuous function `Spec A‚Å∞_f ‚Üí Proj|D(f)` sending `q` to `{a | a·µ¢·µê/f‚Å± ‚àà q}` where
`m` is the degree of `f` -/
def fromSpec {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m) (hm : 0 < m) :
    (Spec.T (A‚Å∞_ f)) ‚ü∂ (Proj.T| (pbo f)) where
  toFun := FromSpec.toFun f_deg hm
  continuous_toFun := by
    rw [isTopologicalBasis_subtype (ProjectiveSpectrum.isTopologicalBasis_basic_opens ùíú) (pbo f).1
      |>.continuous_iff]
    rintro s ‚ü®_, ‚ü®a, rfl‚ü©, rfl‚ü©
    have h‚ÇÅ : Subtype.val (p := (pbo f).1) ‚Åª¬π' (pbo a) =
        ‚ãÉ i : ‚Ñï, Subtype.val (p := (pbo f).1) ‚Åª¬π' (pbo (decompose ùíú a i)) := by
      simp [ProjectiveSpectrum.basicOpen_eq_union_of_projection ùíú a]
    let e : _ ‚âÉ _ :=
      ‚ü®FromSpec.toFun f_deg hm, ToSpec.toFun f, toSpec_fromSpec _ _ _, fromSpec_toSpec _ _ _‚ü©
    change IsOpen <| e ‚Åª¬π' _
    rw [Set.preimage_equiv_eq_image_symm, h‚ÇÅ, Set.image_iUnion]
    exact isOpen_iUnion fun i ‚Ü¶ toSpec.image_basicOpen_eq_basicOpen f_deg hm a i ‚ñ∏
      PrimeSpectrum.isOpen_basicOpen

end ProjIsoSpecTopComponent

variable {ùíú} in
/--
The homeomorphism `Proj|D(f) ‚âÖ Spec A‚Å∞_f` defined by
- `œÜ : Proj|D(f) ‚ü∂ Spec A‚Å∞_f` by sending `x` to `A‚Å∞_f ‚à© span {g / 1 | g ‚àà x}`
- `œà : Spec A‚Å∞_f ‚ü∂ Proj|D(f)` by sending `q` to `{a | a·µ¢·µê/f‚Å± ‚àà q}`.
-/
def projIsoSpecTopComponent {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m) (hm : 0 < m) :
    (Proj.T| (pbo f)) ‚âÖ (Spec.T (A‚Å∞_ f))  where
  hom := ProjIsoSpecTopComponent.toSpec ùíú f
  inv := ProjIsoSpecTopComponent.fromSpec f_deg hm
  hom_inv_id := ConcreteCategory.hom_ext _ _
    (ProjIsoSpecTopComponent.fromSpec_toSpec ùíú f_deg hm)
  inv_hom_id := ConcreteCategory.hom_ext _ _
    (ProjIsoSpecTopComponent.toSpec_fromSpec ùíú f_deg hm)

namespace ProjectiveSpectrum.Proj

/--
The ring map from `A‚Å∞_ f` to the local sections of the structure sheaf of the projective spectrum of
`A` on the basic open set `D(f)` defined by sending `s ‚àà A‚Å∞_f` to the section `x ‚Ü¶ s` on `D(f)`.
-/
def awayToSection (f) : CommRingCat.of (A‚Å∞_ f) ‚ü∂ (structureSheaf ùíú).1.obj (op (pbo f)) where
  toFun s :=
    ‚ü®fun x ‚Ü¶ HomogeneousLocalization.mapId ùíú (Submonoid.powers_le.mpr x.2) s, fun x ‚Ü¶ by
      obtain ‚ü®s, rfl‚ü© := HomogeneousLocalization.mk_surjective s
      obtain ‚ü®n, hn : f ^ n = s.den.1‚ü© := s.den_mem
      exact ‚ü®_, x.2, ùüô _, s.1, s.2, s.3,
        fun x hsx ‚Ü¶ x.2 (Ideal.IsPrime.mem_of_pow_mem inferInstance n (hn ‚ñ∏ hsx)), fun _ ‚Ü¶ rfl‚ü©‚ü©
  map_add' _ _ := by ext; simp only [map_add, HomogeneousLocalization.val_add, Proj.add_apply]
  map_mul' _ _ := by ext; simp only [map_mul, HomogeneousLocalization.val_mul, Proj.mul_apply]
  map_zero' := by ext; simp only [map_zero, HomogeneousLocalization.val_zero, Proj.zero_apply]
  map_one' := by ext; simp only [map_one, HomogeneousLocalization.val_one, Proj.one_apply]

lemma awayToSection_germ (f x) :
    awayToSection ùíú f ‚â´ (structureSheaf ùíú).presheaf.germ x =
      (HomogeneousLocalization.mapId ùíú (Submonoid.powers_le.mpr x.2)) ‚â´
        (Proj.stalkIso' ùíú x).toCommRingCatIso.inv := by
  ext z
  apply (Proj.stalkIso' ùíú x).eq_symm_apply.mpr
  apply Proj.stalkIso'_germ

/--
The ring map from `A‚Å∞_ f` to the global sections of the structure sheaf of the projective spectrum
of `A` restricted to the basic open set `D(f)`.

Mathematically, the map is the same as `awayToSection`.
-/
def awayToŒì (f) : CommRingCat.of (A‚Å∞_ f) ‚ü∂ LocallyRingedSpace.Œì.obj (op <| Proj| pbo f) :=
  awayToSection ùíú f ‚â´ (ProjectiveSpectrum.Proj.structureSheaf ùíú).1.map
    (homOfLE (Opens.openEmbedding_obj_top _).le).op

lemma awayToŒì_ŒìToStalk (f) (x) :
    awayToŒì ùíú f ‚â´ (Proj| pbo f).presheaf.Œìgerm x =
      HomogeneousLocalization.mapId ùíú (Submonoid.powers_le.mpr x.2) ‚â´
      (Proj.stalkIso' ùíú x.1).toCommRingCatIso.inv ‚â´
      ((Proj.toLocallyRingedSpace ùíú).restrictStalkIso (Opens.openEmbedding _) x).inv := by
  rw [awayToŒì, Category.assoc, ‚Üê Category.assoc _ (Iso.inv _),
    Iso.eq_comp_inv, Category.assoc, Category.assoc, Presheaf.Œìgerm]
  rw [LocallyRingedSpace.restrictStalkIso_hom_eq_germ]
  simp only [Proj.toLocallyRingedSpace, Proj.toSheafedSpace]
  rw [Presheaf.germ_res, awayToSection_germ]
  rfl

/--
The morphism of locally ringed space from `Proj|D(f)` to `Spec A‚Å∞_f` induced by the ring map
`A‚Å∞_ f ‚Üí Œì(Proj, D(f))` under the gamma spec adjunction.
-/
def toSpec (f) : (Proj| pbo f) ‚ü∂ Spec (A‚Å∞_ f) :=
  ŒìSpec.locallyRingedSpaceAdjunction.homEquiv (Proj| pbo f) (op (CommRingCat.of <| A‚Å∞_ f))
    (awayToŒì ùíú f).op

open HomogeneousLocalization LocalRing

lemma toSpec_base_apply_eq_comap {f} (x : Proj| pbo f) :
    (toSpec ùíú f).1.base x = PrimeSpectrum.comap (mapId ùíú (Submonoid.powers_le.mpr x.2))
      (closedPoint (AtPrime ùíú x.1.asHomogeneousIdeal.toIdeal)) := by
  show PrimeSpectrum.comap (awayToŒì ùíú f ‚â´ (Proj| pbo f).presheaf.Œìgerm x)
        (LocalRing.closedPoint ((Proj| pbo f).presheaf.stalk x)) = _
  rw [awayToŒì_ŒìToStalk, CommRingCat.comp_eq_ring_hom_comp, PrimeSpectrum.comap_comp]
  exact congr(PrimeSpectrum.comap _ $(@LocalRing.comap_closedPoint
    (HomogeneousLocalization.AtPrime ùíú x.1.asHomogeneousIdeal.toIdeal) _ _
    ((Proj| pbo f).presheaf.stalk x) _ _ _ (isLocalRingHom_of_isIso _)))

lemma toSpec_base_apply_eq {f} (x : Proj| pbo f) :
    (toSpec ùíú f).1.base x = ProjIsoSpecTopComponent.toSpec ùíú f x :=
  toSpec_base_apply_eq_comap ùíú x |>.trans <| PrimeSpectrum.ext <| Ideal.ext fun z =>
  show ¬¨ IsUnit _ ‚Üî z ‚àà ProjIsoSpecTopComponent.ToSpec.carrier _ by
  obtain ‚ü®z, rfl‚ü© := z.mk_surjective
  rw [‚Üê HomogeneousLocalization.isUnit_iff_isUnit_val,
    ProjIsoSpecTopComponent.ToSpec.mk_mem_carrier, HomogeneousLocalization.map_mk,
    HomogeneousLocalization.val_mk, Localization.mk_eq_mk',
    IsLocalization.AtPrime.isUnit_mk'_iff]
  exact not_not

lemma toSpec_base_isIso {f} {m} (f_deg : f ‚àà ùíú m) (hm : 0 < m) :
    IsIso (toSpec ùíú f).1.base := by
  convert (projIsoSpecTopComponent f_deg hm).isIso_hom
  exact DFunLike.ext _ _ <| toSpec_base_apply_eq ùíú

lemma mk_mem_toSpec_base_apply {f} (x : Proj| pbo f)
    (z : NumDenSameDeg ùíú (.powers f)) :
    HomogeneousLocalization.mk z ‚àà ((toSpec ùíú f).1.base x).asIdeal ‚Üî
      z.num.1 ‚àà x.1.asHomogeneousIdeal :=
  (toSpec_base_apply_eq ùíú x).symm ‚ñ∏ ProjIsoSpecTopComponent.ToSpec.mk_mem_carrier _ _

lemma toSpec_preimage_basicOpen {f}
    (t : NumDenSameDeg ùíú (.powers f)) :
    (Opens.map (toSpec ùíú f).1.base).obj (sbo (.mk t)) =
      Opens.comap ‚ü®_, continuous_subtype_val‚ü© (pbo t.num.1) :=
  Opens.ext <| Opens.map_coe _ _ ‚ñ∏ by
  convert (ProjIsoSpecTopComponent.ToSpec.preimage_basicOpen f t)
  exact funext fun _ => toSpec_base_apply_eq _ _

@[reassoc]
lemma toOpen_toSpec_val_c_app (f) (U) :
    StructureSheaf.toOpen (A‚Å∞_ f) U.unop ‚â´ (toSpec ùíú f).val.c.app U =
      awayToŒì ùíú f ‚â´ (Proj| pbo f).presheaf.map (homOfLE le_top).op :=
  Eq.trans (by congr) <| ŒìSpec.toOpen_comp_locallyRingedSpaceAdjunction_homEquiv_app _ U

@[reassoc]
lemma toStalk_stalkMap_toSpec (f) (x) :
    StructureSheaf.toStalk _ _ ‚â´ (toSpec ùíú f).stalkMap x =
      awayToŒì ùíú f ‚â´ (Proj| pbo f).presheaf.Œìgerm x := by
  rw [StructureSheaf.toStalk, Category.assoc]
  simp_rw [CommRingCat.coe_of]
  erw [PresheafedSpace.stalkMap_germ']
  rw [toOpen_toSpec_val_c_app_assoc, Presheaf.germ_res]
  rfl

/--
If `x` is a point in the basic open set `D(f)` where `f` is a homogeneous element of positive
degree, then the homogeneously localized ring `A‚Å∞‚Çì` has the universal property of the localization
of `A‚Å∞_f` at `œÜ(x)` where `œÜ : Proj|D(f) ‚ü∂ Spec A‚Å∞_f` is the morphism of locally ringed space
constructed as above.
-/
lemma isLocalization_atPrime (f) (x : pbo f) {m} (f_deg : f ‚àà ùíú m) (hm : 0 < m) :
    @IsLocalization (Away ùíú f) _ ((toSpec ùíú f).1.base x).asIdeal.primeCompl
      (AtPrime ùíú x.1.asHomogeneousIdeal.toIdeal) _
      (mapId ùíú (Submonoid.powers_le.mpr x.2)).toAlgebra := by
  letI : Algebra (Away ùíú f) (AtPrime ùíú x.1.asHomogeneousIdeal.toIdeal) :=
    (mapId ùíú (Submonoid.powers_le.mpr x.2)).toAlgebra
  constructor
  ¬∑ rintro ‚ü®y, hy‚ü©
    obtain ‚ü®y, rfl‚ü© := y.mk_surjective
    refine isUnit_of_mul_eq_one _
      (.mk ‚ü®y.deg, y.den, y.num, (mk_mem_toSpec_base_apply _ _ _).not.mp hy‚ü©) <| val_injective _ ?_
    simp only [RingHom.algebraMap_toAlgebra, map_mk, RingHom.id_apply, val_mul, val_mk, mk_eq_mk',
      val_one, IsLocalization.mk'_mul_mk'_eq_one']
  ¬∑ intro z
    obtain ‚ü®‚ü®i, a, ‚ü®b, hb‚ü©, (hb' : b ‚àâ x.1.1)‚ü©, rfl‚ü© := z.mk_surjective
    refine ‚ü®‚ü®.mk ‚ü®i * m, ‚ü®a * b ^ (m - 1), ?_‚ü©, ‚ü®f ^ i, SetLike.pow_mem_graded _ f_deg‚ü©, ‚ü®_, rfl‚ü©‚ü©,
      ‚ü®.mk ‚ü®i * m, ‚ü®b ^ m, mul_comm m i ‚ñ∏ SetLike.pow_mem_graded _ hb‚ü©,
        ‚ü®f ^ i, SetLike.pow_mem_graded _ f_deg‚ü©, ‚ü®_, rfl‚ü©‚ü©,
        (mk_mem_toSpec_base_apply _ _ _).not.mpr <| x.1.1.toIdeal.primeCompl.pow_mem hb' m‚ü©‚ü©,
        val_injective _ ?_‚ü©
    ¬∑ convert SetLike.mul_mem_graded a.2 (SetLike.pow_mem_graded (m - 1) hb) using 2
      rw [‚Üê succ_nsmul', tsub_add_cancel_of_le (by omega), mul_comm, smul_eq_mul]
    ¬∑ simp only [RingHom.algebraMap_toAlgebra, map_mk, RingHom.id_apply, val_mul, val_mk,
        mk_eq_mk', ‚Üê IsLocalization.mk'_mul, Submonoid.mk_mul_mk, IsLocalization.mk'_eq_iff_eq]
      rw [mul_comm b, mul_mul_mul_comm, ‚Üê pow_succ', mul_assoc, tsub_add_cancel_of_le (by omega)]
  ¬∑ intros y z e
    obtain ‚ü®y, rfl‚ü© := y.mk_surjective
    obtain ‚ü®z, rfl‚ü© := z.mk_surjective
    obtain ‚ü®i, c, hc, hc', e‚ü© : ‚àÉ i, ‚àÉ c ‚àà ùíú i, c ‚àâ x.1.asHomogeneousIdeal ‚àß
        c * (z.den.1 * y.num.1) = c * (y.den.1 * z.num.1) := by
      apply_fun HomogeneousLocalization.val at e
      simp only [RingHom.algebraMap_toAlgebra, map_mk, RingHom.id_apply, val_mk, mk_eq_mk',
        IsLocalization.mk'_eq_iff_eq] at e
      obtain ‚ü®‚ü®c, hcx‚ü©, hc‚ü© := IsLocalization.exists_of_eq (M := x.1.1.toIdeal.primeCompl) e
      obtain ‚ü®i, hi‚ü© := not_forall.mp ((x.1.1.isHomogeneous.mem_iff _).not.mp hcx)
      refine ‚ü®i, _, (decompose ùíú c i).2, hi, ?_‚ü©
      apply_fun fun x ‚Ü¶ (decompose ùíú x (i + z.deg + y.deg)).1 at hc
      conv_rhs at hc => rw [add_right_comm]
      rwa [‚Üê mul_assoc, coe_decompose_mul_add_of_right_mem, coe_decompose_mul_add_of_right_mem,
        ‚Üê mul_assoc, coe_decompose_mul_add_of_right_mem, coe_decompose_mul_add_of_right_mem,
        mul_assoc, mul_assoc] at hc
      exacts [y.den.2, z.num.2, z.den.2, y.num.2]

    refine ‚ü®‚ü®.mk ‚ü®m * i, ‚ü®c ^ m, SetLike.pow_mem_graded _ hc‚ü©,
      ‚ü®f ^ i, mul_comm m i ‚ñ∏ SetLike.pow_mem_graded _ f_deg‚ü©, ‚ü®_, rfl‚ü©‚ü©,
      (mk_mem_toSpec_base_apply _ _ _).not.mpr <| x.1.1.toIdeal.primeCompl.pow_mem hc' _‚ü©,
      val_injective _ ?_‚ü©
    simp only [val_mul, val_mk, mk_eq_mk', ‚Üê IsLocalization.mk'_mul, Submonoid.mk_mul_mk,
      IsLocalization.mk'_eq_iff_eq, mul_assoc]
    congr 2
    rw [mul_left_comm, mul_left_comm y.den.1, ‚Üê tsub_add_cancel_of_le (show 1 ‚â§ m from hm),
      pow_succ, mul_assoc, mul_assoc, e]

/--
For an element `f ‚àà A` with positive degree and a homogeneous ideal in `D(f)`, we have that the
stalk of `Spec A‚Å∞_ f` at `y` is isomorphic to `A‚Å∞‚Çì` where `y` is the point in `Proj` corresponding
to `x`.
-/
def specStalkEquiv (f) (x : pbo f) {m} (f_deg : f ‚àà ùíú m) (hm : 0 < m) :
    (Spec.structureSheaf (A‚Å∞_ f)).presheaf.stalk ((toSpec ùíú f).1.base x) ‚âÖ
      CommRingCat.of (AtPrime ùíú x.1.asHomogeneousIdeal.toIdeal) :=
  letI : Algebra (Away ùíú f) (AtPrime ùíú x.1.asHomogeneousIdeal.toIdeal) :=
    (mapId ùíú (Submonoid.powers_le.mpr x.2)).toAlgebra
  haveI := isLocalization_atPrime ùíú f x f_deg hm
  (IsLocalization.algEquiv
    (R := A‚Å∞_ f)
    (M := ((toSpec ùíú f).1.base x).asIdeal.primeCompl)
    (S := (Spec.structureSheaf (A‚Å∞_ f)).presheaf.stalk ((toSpec ùíú f).1.base x))
    (Q := AtPrime ùíú x.1.asHomogeneousIdeal.toIdeal)).toRingEquiv.toCommRingCatIso

lemma toStalk_specStalkEquiv (f) (x : pbo f) {m} (f_deg : f ‚àà ùíú m) (hm : 0 < m) :
    StructureSheaf.toStalk (A‚Å∞_ f) ((toSpec ùíú f).1.base x) ‚â´ (specStalkEquiv ùíú f x f_deg hm).hom =
      (mapId _ <| Submonoid.powers_le.mpr x.2 : (A‚Å∞_ f) ‚Üí+* AtPrime ùíú x.1.1.toIdeal) :=
  letI : Algebra (Away ùíú f) (AtPrime ùíú x.1.asHomogeneousIdeal.toIdeal) :=
    (mapId ùíú (Submonoid.powers_le.mpr x.2)).toAlgebra
  letI := isLocalization_atPrime ùíú f x f_deg hm
  (IsLocalization.algEquiv
    (R := A‚Å∞_ f)
    (M := ((toSpec ùíú f).1.base x).asIdeal.primeCompl)
    (S := (Spec.structureSheaf (A‚Å∞_ f)).presheaf.stalk ((toSpec ùíú f).1.base x))
    (Q := AtPrime ùíú x.1.asHomogeneousIdeal.toIdeal)).toAlgHom.comp_algebraMap

lemma stalkMap_toSpec (f) (x : pbo f) {m} (f_deg : f ‚àà ùíú m) (hm : 0 < m) :
    (toSpec ùíú f).stalkMap x =
      (specStalkEquiv ùíú f x f_deg hm).hom ‚â´ (Proj.stalkIso' ùíú x.1).toCommRingCatIso.inv ‚â´
      ((Proj.toLocallyRingedSpace ùíú).restrictStalkIso (Opens.openEmbedding _) x).inv :=
  IsLocalization.ringHom_ext (R := A‚Å∞_ f) ((toSpec ùíú f).1.base x).asIdeal.primeCompl
    (S := (Spec.structureSheaf (A‚Å∞_ f)).presheaf.stalk ((toSpec ùíú f).1.base x)) <|
    (toStalk_stalkMap_toSpec _ _ _).trans <| by
    rw [awayToŒì_ŒìToStalk, ‚Üê toStalk_specStalkEquiv, Category.assoc]; rfl

lemma isIso_toSpec (f) {m} (f_deg : f ‚àà ùíú m) (hm : 0 < m) :
    IsIso (toSpec ùíú f) := by
  haveI : IsIso (toSpec ùíú f).1.base := toSpec_base_isIso ùíú f_deg hm
  haveI (x) : IsIso ((toSpec ùíú f).stalkMap x) := by
    rw [stalkMap_toSpec ùíú f x f_deg hm]; infer_instance
  haveI : LocallyRingedSpace.IsOpenImmersion (toSpec ùíú f) :=
    LocallyRingedSpace.IsOpenImmersion.of_stalk_iso (toSpec ùíú f)
      (TopCat.homeoOfIso (asIso <| (toSpec ùíú f).1.base)).openEmbedding
  exact LocallyRingedSpace.IsOpenImmersion.to_iso _

end ProjectiveSpectrum.Proj

open ProjectiveSpectrum.Proj in
/--
If `f ‚àà A` is a homogeneous element of positive degree, then the projective spectrum restricted to
`D(f)` as a locally ringed space is isomorphic to `Spec A‚Å∞_f`.
-/
def projIsoSpec (f) {m} (f_deg : f ‚àà ùíú m) (hm : 0 < m) :
    (Proj| pbo f) ‚âÖ (Spec (A‚Å∞_ f)) :=
  @asIso (f := toSpec ùíú f) (isIso_toSpec ùíú f f_deg hm)

/--
This is the scheme `Proj(A)` for any `‚Ñï`-graded ring `A`.
-/
def ¬´Proj¬ª : Scheme where
  __ := Proj.toLocallyRingedSpace ùíú
  local_affine (x : Proj.T) := by
    classical
    obtain ‚ü®f, m, f_deg, hm, hx‚ü© : ‚àÉ (f : A) (m : ‚Ñï) (_ : f ‚àà ùíú m) (_ : 0 < m), f ‚àâ x.1 := by
      by_contra!
      refine x.not_irrelevant_le fun z hz ‚Ü¶ ?_
      rw [‚Üê DirectSum.sum_support_decompose ùíú z]
      exact x.1.toIdeal.sum_mem fun k hk ‚Ü¶ this _ k (SetLike.coe_mem _) <| by_contra <| by aesop
    exact ‚ü®‚ü®pbo f, hx‚ü©, .of (A‚Å∞_ f), ‚ü®projIsoSpec ùíú f f_deg hm‚ü©‚ü©


end AlgebraicGeometry
