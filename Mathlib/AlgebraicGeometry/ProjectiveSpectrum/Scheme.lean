/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import Mathlib.AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf
import Mathlib.AlgebraicGeometry.Spec
import Mathlib.RingTheory.GradedAlgebra.Radical

#align_import algebraic_geometry.projective_spectrum.scheme from "leanprover-community/mathlib"@"d39590fc8728fbf6743249802486f8c91ffe07bc"

/-!
# Proj as a scheme

This file is to prove that `Proj` is a scheme.

## Notation

* `Proj`      : `Proj` as a locally ringed space
* `Proj.T`    : the underlying topological space of `Proj`
* `Proj| U`   : `Proj` restricted to some open set `U`
* `Proj.T| U` : the underlying topological space of `Proj` restricted to open set `U`
* `pbo f`     : basic open set at `f` in `Proj`
* `Spec`      : `Spec` as a locally ringed space
* `Spec.T`    : the underlying topological space of `Spec`
* `sbo g`     : basic open set at `g` in `Spec`
* `A‚Å∞_x`      : the degree zero part of localized ring `A‚Çì`

## Implementation

In `AlgebraicGeometry/ProjectiveSpectrum/StructureSheaf.lean`, we have given `Proj` a
structure sheaf so that `Proj` is a locally ringed space. In this file we will prove that `Proj`
equipped with this structure sheaf is a scheme. We achieve this by using an affine cover by basic
open sets in `Proj`, more specifically:

1. We prove that `Proj` can be covered by basic open sets at homogeneous element of positive degree.
2. We prove that for any homogeneous element `f : A` of positive degree `m`, `Proj.T | (pbo f)` is
    homeomorphic to `Spec.T A‚Å∞_f`:
  - forward direction `toSpec`:
    for any `x : pbo f`, i.e. a relevant homogeneous prime ideal `x`, send it to
    `A‚Å∞_f ‚à© span {g / 1 | g ‚àà x}` (see `ProjIsoSpecTopComponent.IoSpec.carrier`). This ideal is
    prime, the proof is in `ProjIsoSpecTopComponent.ToSpec.toFun`. The fact that this function
    is continuous is found in `ProjIsoSpecTopComponent.toSpec`
  - backward direction `fromSpec`:
    for any `q : Spec A‚Å∞_f`, we send it to `{a | ‚àÄ i, a·µ¢·µê/f‚Å± ‚àà q}`; we need this to be a
    homogeneous prime ideal that is relevant.
    * This is in fact an ideal, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal`;
    * This ideal is also homogeneous, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal.homogeneous`;
    * This ideal is relevant, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.relevant`;
    * This ideal is prime, the proof can be found in
      `ProjIsoSpecTopComponent.FromSpec.carrier.prime`.
    Hence we have a well defined function `Spec.T A‚Å∞_f ‚Üí Proj.T | (pbo f)`, this function is called
    `ProjIsoSpecTopComponent.FromSpec.toFun`. But to prove the continuity of this function,
    we need to prove `fromSpec ‚àò toSpec` and `toSpec ‚àò fromSpec` are both identities (TBC).

## Main Definitions and Statements

* `degreeZeroPart`: the degree zero part of the localized ring `A‚Çì` where `x` is a homogeneous
  element of degree `n` is the subring of elements of the form `a/f^m` where `a` has degree `mn`.

For a homogeneous element `f` of degree `n`
* `ProjIsoSpecTopComponent.toSpec`: `forward f` is the
  continuous map between `Proj.T| pbo f` and `Spec.T A‚Å∞_f`
* `ProjIsoSpecTopComponent.ToSpec.preimage_eq`: for any `a: A`, if `a/f^m` has degree zero,
  then the preimage of `sbo a/f^m` under `to_Spec f` is `pbo f ‚à© pbo a`.

* [Robin Hartshorne, *Algebraic Geometry*][Har77]: Chapter II.2 Proposition 2.5
-/


noncomputable section

set_option linter.uppercaseLean3 false -- Porting note: Proj and Spec

namespace AlgebraicGeometry

open scoped DirectSum BigOperators Pointwise BigOperators

open DirectSum SetLike.GradedMonoid Localization

open Finset hiding mk_zero

variable {R A : Type*}

variable [CommRing R] [CommRing A] [Algebra R A]

variable (ùíú : ‚Ñï ‚Üí Submodule R A)

variable [GradedAlgebra ùíú]

open TopCat TopologicalSpace

open CategoryTheory Opposite

open ProjectiveSpectrum.StructureSheaf

-- Porting note: currently require lack of hygiene to use in variable declarations
-- maybe all make into notation3?
set_option hygiene false
-- `Proj` as a locally ringed space
local notation3 "Proj" => Proj.toLocallyRingedSpace ùíú

-- the underlying topological space of `Proj`
local notation3 "Proj.T" => PresheafedSpace.carrier <| SheafedSpace.toPresheafedSpace
  <| LocallyRingedSpace.toSheafedSpace <| Proj.toLocallyRingedSpace ùíú

-- `Proj` restrict to some open set
macro "Proj| " U:term : term =>
  `((Proj.toLocallyRingedSpace ùíú).restrict (Opens.openEmbedding (X := Proj.T) ($U : Opens Proj.T)))

-- the underlying topological space of `Proj` restricted to some open set
local notation "Proj.T| " U => PresheafedSpace.carrier <| SheafedSpace.toPresheafedSpace
  <| LocallyRingedSpace.toSheafedSpace
    <| (LocallyRingedSpace.restrict Proj (Opens.openEmbedding (X := Proj.T) (U : Opens Proj.T)))

-- basic open sets in `Proj`
local notation "pbo " x => ProjectiveSpectrum.basicOpen ùíú x

-- basic open sets in `Spec`
local notation "sbo " f => PrimeSpectrum.basicOpen f

-- `Spec` as a locally ringed space
local notation3 "Spec " ring => Spec.locallyRingedSpaceObj (CommRingCat.of ring)

-- the underlying topological space of `Spec`
local notation "Spec.T " ring =>
  (Spec.locallyRingedSpaceObj (CommRingCat.of ring)).toSheafedSpace.toPresheafedSpace.1

local notation3 "A‚Å∞_ " f => HomogeneousLocalization.Away ùíú f

namespace ProjIsoSpecTopComponent

/-
This section is to construct the homeomorphism between `Proj` restricted at basic open set at
a homogeneous element `x` and `Spec A‚Å∞‚Çì` where `A‚Å∞‚Çì` is the degree zero part of the localized
ring `A‚Çì`.
-/
namespace ToSpec

open Ideal

-- This section is to construct the forward direction :
-- So for any `x` in `Proj| (pbo f)`, we need some point in `Spec A‚Å∞_f`, i.e. a prime ideal,
-- and we need this correspondence to be continuous in their Zariski topology.
variable {ùíú} {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m) (x : Proj| (pbo f))

/--
For any `x` in `Proj| (pbo f)`, the corresponding ideal in `Spec A‚Å∞_f`. This fact that this ideal
is prime is proven in `TopComponent.Forward.toFun`-/
def carrier : Ideal (A‚Å∞_ f) :=
  Ideal.comap (algebraMap (A‚Å∞_ f) (Away f))
    (Ideal.span <| algebraMap A (Away f) '' x.val.asHomogeneousIdeal)
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec.carrier AlgebraicGeometry.ProjIsoSpecTopComponent.ToSpec.carrier

theorem mem_carrier_iff (z : A‚Å∞_ f) :
    z ‚àà carrier x ‚Üî z.val ‚àà Ideal.span (algebraMap A (Away f) '' x.1.asHomogeneousIdeal) :=
  Iff.rfl
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec.mem_carrier_iff AlgebraicGeometry.ProjIsoSpecTopComponent.ToSpec.mem_carrier_iff

theorem MemCarrier.clear_denominator' [DecidableEq (Away f)] {z : Localization.Away f}
    (hz : z ‚àà span (algebraMap A (Away f) '' x.val.asHomogeneousIdeal)) :
    ‚àÉ (c : algebraMap A (Away f) '' x.1.asHomogeneousIdeal ‚Üí‚ÇÄ Away f) (N : ‚Ñï) (acd :
      ‚àÄ y ‚àà c.support.image c, A),
      f ^ N ‚Ä¢ z =
        algebraMap A (Away f)
          (‚àë i in c.support.attach,
            acd (c i) (Finset.mem_image.mpr ‚ü®i, ‚ü®i.2, rfl‚ü©‚ü©) * i.1.2.choose) := by
  rw [‚Üê submodule_span_eq, Finsupp.span_eq_range_total, LinearMap.mem_range] at hz
  rcases hz with ‚ü®c, eq1‚ü©
  rw [Finsupp.total_apply, Finsupp.sum] at eq1
  obtain ‚ü®‚ü®_, N, rfl‚ü©, hN‚ü© :=
    IsLocalization.exist_integer_multiples_of_finset (Submonoid.powers f) (c.support.image c)
  choose acd hacd using hN
  refine' ‚ü®c, N, acd, _‚ü©
  rw [‚Üê eq1, smul_sum, map_sum, ‚Üê sum_attach]
  congr 1
  ext i
  rw [_root_.map_mul, hacd, (Classical.choose_spec i.1.2).2, smul_eq_mul, smul_mul_assoc]
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec.mem_carrier.clear_denominator' AlgebraicGeometry.ProjIsoSpecTopComponent.ToSpec.MemCarrier.clear_denominator'

theorem MemCarrier.clear_denominator [DecidableEq (Away f)] {z : A‚Å∞_ f} (hz : z ‚àà carrier x) :
    ‚àÉ (c : algebraMap A (Away f) '' x.1.asHomogeneousIdeal ‚Üí‚ÇÄ Away f) (N : ‚Ñï) (acd :
      ‚àÄ y ‚àà c.support.image c, A),
      f ^ N ‚Ä¢ z.val =
        algebraMap A (Away f)
          (‚àë i in c.support.attach,
            acd (c i) (Finset.mem_image.mpr ‚ü®i, ‚ü®i.2, rfl‚ü©‚ü©) * i.1.2.choose) :=
  MemCarrier.clear_denominator' x <| (mem_carrier_iff x z).mpr hz
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec.mem_carrier.clear_denominator AlgebraicGeometry.ProjIsoSpecTopComponent.ToSpec.MemCarrier.clear_denominator

/--
For `x : pbo f`
The ideal `A‚Å∞_f ‚à© span {g / 1 | g ‚àà x}` is equal to `span {a/f^n | a ‚àà x and deg(a) = deg(f^n)}`
-/
lemma carrier_eq_span :
    carrier x =
    Ideal.span { z : HomogeneousLocalization.Away ùíú f |
      ‚àÉ (s F : A) (_ : s ‚àà x.1.asHomogeneousIdeal) (n : ‚Ñï)
        (s_mem : s ‚àà ùíú n) (F_mem1 : F ‚àà ùíú n) (F_mem2 : F ‚àà Submonoid.powers f),
        z = Quotient.mk'' ‚ü®n, ‚ü®s, s_mem‚ü©, ‚ü®F, F_mem1‚ü©,F_mem2‚ü© } := by
  classical
  refine le_antisymm ?_ <| Ideal.span_le.mpr ?_
  ¬∑ intro z hz
    let k : ‚Ñï := z.den_mem.choose
    have hk : f^k = z.den := z.den_mem.choose_spec
    suffices mem1 : z.num ‚àà x.1.asHomogeneousIdeal
    ¬∑ refine Ideal.subset_span ‚ü®_, _, mem1, _, z.num_mem_deg, z.den_mem_deg, z.den_mem, ?_‚ü©
      rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
        HomogeneousLocalization.eq_num_div_den]

    obtain ‚ü®c, N, acd, eq1‚ü© := MemCarrier.clear_denominator x hz
    rw [z.eq_num_div_den, smul_mk, smul_eq_mul, ‚Üê mk_one_eq_algebraMap,
      Localization.mk_eq_mk_iff, Localization.r_iff_exists] at eq1
    obtain ‚ü®‚ü®_, ‚ü®l, rfl‚ü©‚ü©, eq1‚ü© := eq1
    dsimp only [OneMemClass.coe_one] at eq1
    rw [one_mul, ‚Üê hk, ‚Üê mul_assoc, ‚Üê mul_assoc, ‚Üê pow_add, ‚Üê pow_add] at eq1

    suffices : f^(l + k) * ‚àë i in c.support.attach, acd (c i) _ * i.1.2.choose ‚àà
      x.1.asHomogeneousIdeal
    ¬∑ exact (x.1.isPrime.mem_or_mem (eq1.symm ‚ñ∏ this)).resolve_left fun r ‚Ü¶
        (ProjectiveSpectrum.mem_basicOpen ùíú _ _).mp x.2 <| x.1.isPrime.mem_of_pow_mem _ r
    exact Ideal.mul_mem_left _ _ <| Ideal.sum_mem _ (fun ‚ü®j, hj‚ü© _ ‚Ü¶
      Ideal.mul_mem_left _ _ j.2.choose_spec.1)

  ¬∑ rintro _ ‚ü®s, _, hs, n, s_mem, F_mem, ‚ü®l, rfl‚ü©, rfl‚ü©
    erw [mem_carrier_iff, HomogeneousLocalization.val_mk'']
    erw [show (Localization.mk s ‚ü®f ^ l, ‚ü®_, rfl‚ü©‚ü© : Localization.Away f) =
      (Localization.mk 1 ‚ü®f^l, ‚ü®_, rfl‚ü©‚ü© : Localization.Away f) * Localization.mk s 1 by
      ¬∑ rw [Localization.mk_mul, mul_one, one_mul]]
    exact Ideal.mul_mem_left _ _ <| Ideal.subset_span ‚ü®s, hs, rfl‚ü©

theorem disjoint :
    Disjoint (x.1.asHomogeneousIdeal.toIdeal : Set A) (Submonoid.powers f : Set A) := by
  by_contra rid
  rw [Set.not_disjoint_iff] at rid
  choose g hg using rid
  obtain ‚ü®hg1, ‚ü®k, rfl‚ü©‚ü© := hg
  by_cases k_ineq : 0 < k
  ¬∑ erw [x.1.isPrime.pow_mem_iff_mem _ k_ineq] at hg1
    exact x.2 hg1
  ¬∑ dsimp at hg1
    erw [show k = 0 by linarith, pow_zero, ‚Üê Ideal.eq_top_iff_one] at hg1
    apply x.1.isPrime.1
    exact hg1
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec.disjoint AlgebraicGeometry.ProjIsoSpecTopComponent.ToSpec.disjoint

theorem carrier_ne_top : carrier x ‚â† ‚ä§ := by
  have eq_top := disjoint x
  classical
  contrapose! eq_top
  obtain ‚ü®c, N, acd, eq1‚ü© := MemCarrier.clear_denominator x ((Ideal.eq_top_iff_one _).mp eq_top)
  rw [Algebra.smul_def, HomogeneousLocalization.one_val, mul_one] at eq1
  change Localization.mk (f ^ N) 1 = Localization.mk _ 1 at eq1
  simp only [mk_eq_mk', IsLocalization.eq] at eq1
  rcases eq1 with ‚ü®‚ü®_, ‚ü®M, rfl‚ü©‚ü©, eq1‚ü©
  dsimp at eq1
  erw [one_mul, one_mul] at eq1
  change f ^ _ * f ^ _ = f ^ _ * _ at eq1
  rw [Set.not_disjoint_iff_nonempty_inter]
  refine'
    ‚ü®f ^ M * f ^ N, eq1.symm ‚ñ∏ mul_mem_left _ _ (sum_mem _ fun i _ => mul_mem_left _ _ _),
      ‚ü®M + N, by dsimp; rw [pow_add]‚ü©‚ü©
  generalize_proofs h‚ÇÅ h‚ÇÇ
  exact (Classical.choose_spec h‚ÇÇ).1
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec.carrier_ne_top AlgebraicGeometry.ProjIsoSpecTopComponent.ToSpec.carrier_ne_top

variable (f)

/-- The function between the basic open set `D(f)` in `Proj` to the corresponding basic open set in
`Spec A‚Å∞_f`. This is bundled into a continuous map in `TopComponent.forward`.
-/
def toFun (x : Proj.T| pbo f) : Spec.T A‚Å∞_ f :=
  ‚ü®carrier x, carrier_ne_top x, fun {x1 x2} hx12 => by
    classical
    rw [mem_carrier_iff] at hx12
    let J := span ((algebraMap A (Away f) : A ‚Üí (Away f)) '' x.val.asHomogeneousIdeal)
    suffices h : ‚àÄ x y : Localization.Away f, x * y ‚àà J ‚Üí x ‚àà J ‚à® y ‚àà J
    ¬∑ rw [HomogeneousLocalization.mul_val] at hx12; exact h x1.val x2.val hx12
    clear x1 x2 hx12
    intro x1 x2 hx12
    induction' x1 using Localization.induction_on with data_x1
    induction' x2 using Localization.induction_on with data_x2
    rcases data_x1, data_x2 with ‚ü®‚ü®a1, _, ‚ü®n1, rfl‚ü©‚ü©, ‚ü®a2, _, ‚ü®n2, rfl‚ü©‚ü©‚ü©
    rcases MemCarrier.clear_denominator' x hx12 with ‚ü®c, N, acd, eq1‚ü©
    simp only [Algebra.smul_def] at eq1
    change Localization.mk (f ^ N) 1 * (Localization.mk _ _ * Localization.mk _ _)
      = Localization.mk _ _ at eq1
    simp only [Localization.mk_mul, one_mul] at eq1
    simp only [mk_eq_mk', IsLocalization.eq] at eq1
    rcases eq1 with ‚ü®‚ü®_, ‚ü®M, rfl‚ü©‚ü©, eq1‚ü©
    rw [Submonoid.coe_one, one_mul] at eq1
    change f ^ _ * (_ * _) = f ^ _ * (f ^ _ * f ^ _ * _) at eq1
    have that : a1 * a2 * f ^ N * f ^ M ‚àà x.val.asHomogeneousIdeal.toIdeal := ?_
    rcases x.1.isPrime.mem_or_mem (show a1 * a2 * f ^ N * f ^ M ‚àà _ from that) with (h1 | rid2)
    rcases x.1.isPrime.mem_or_mem h1 with (h1 | rid1)
    rcases x.1.isPrime.mem_or_mem h1 with (h1 | h2)
    ¬∑ left;
      simp only [show (Localization.mk a1 ‚ü®f ^ n1, _‚ü© : Away f) =
        Localization.mk a1 1 * Localization.mk 1 (‚ü®f ^ n1, ‚ü®n1, rfl‚ü©‚ü© : Submonoid.powers f) by
          rw [Localization.mk_mul, mul_one, one_mul]]
      exact Ideal.mul_mem_right _ _ (Ideal.subset_span ‚ü®_, h1, rfl‚ü©)
    ¬∑ right;
      simp only [show (mk a2 ‚ü®f ^ n2, _‚ü© : Away f) =
        mk a2 1 * Localization.mk 1 (‚ü®f ^ n2, ‚ü®n2, rfl‚ü©‚ü© : Submonoid.powers f) by
          rw [Localization.mk_mul, mul_one, one_mul]]
      exact Ideal.mul_mem_right _ _ (Ideal.subset_span ‚ü®_, h2, rfl‚ü©)
    ¬∑ exact False.elim (x.2 (x.1.isPrime.mem_of_pow_mem N rid1))
    ¬∑ exact False.elim (x.2 (x.1.isPrime.mem_of_pow_mem M rid2))
    ¬∑ rw [‚Üê mul_comm (f ^ M), ‚Üê mul_comm (f ^ N), eq1]
      refine' mul_mem_left _ _ (mul_mem_left _ _ (sum_mem _ fun i _ => mul_mem_left _ _ _))
      generalize_proofs h‚ÇÅ h‚ÇÇ; exact (Classical.choose_spec h‚ÇÇ).1‚ü©
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec.to_fun AlgebraicGeometry.ProjIsoSpecTopComponent.ToSpec.toFun

/-
The preimage of basic open set `D(a/f^n)` in `Spec A‚Å∞_f` under the forward map from `Proj A` to
`Spec A‚Å∞_f` is the basic open set `D(a) ‚à© D(f)` in `Proj A`. This lemma is used to prove that the
forward map is continuous.
-/
theorem preimage_eq (a b : A) (k : ‚Ñï) (a_mem : a ‚àà ùíú k) (b_mem1 : b ‚àà ùíú k)
    (b_mem2 : b ‚àà Submonoid.powers f) :
    toFun f ‚Åª¬π'
        (@PrimeSpectrum.basicOpen (A‚Å∞_ f) _ (Quotient.mk'' ‚ü®k, ‚ü®a, a_mem‚ü©, ‚ü®b, b_mem1‚ü©, b_mem2‚ü©) :
          Set (PrimeSpectrum (HomogeneousLocalization.Away ùíú f))) =
      {x | x.1 ‚àà (pbo f) ‚äì pbo a} := by
  classical
  ext1 y
  constructor <;> intro hy
  ¬∑ refine' ‚ü®y.2, _‚ü©
    rw [Set.mem_preimage, SetLike.mem_coe, PrimeSpectrum.mem_basicOpen] at hy
    rw [ProjectiveSpectrum.mem_coe_basicOpen]
    intro a_mem_y
    apply hy
    rw [toFun, mem_carrier_iff, HomogeneousLocalization.val_mk'', Subtype.coe_mk]
    dsimp; rcases b_mem2 with ‚ü®k, hk‚ü©
    dsimp at hk
    simp only [show (mk a ‚ü®b, ‚ü®k, hk‚ü©‚ü© : Away f) =
      Localization.mk 1 (‚ü®f ^ k, ‚ü®_, rfl‚ü©‚ü© : Submonoid.powers f) * mk a 1 by
        rw [mk_mul, one_mul, mul_one]; congr; rw [hk]]
    exact Ideal.mul_mem_left _ _ (Ideal.subset_span ‚ü®_, a_mem_y, rfl‚ü©)
  ¬∑ change y.1 ‚àà ProjectiveSpectrum.basicOpen ùíú f ‚äì ProjectiveSpectrum.basicOpen ùíú a at hy
    rcases hy with ‚ü®hy1, hy2‚ü©
    rw [ProjectiveSpectrum.mem_coe_basicOpen] at hy1 hy2
    rw [Set.mem_preimage, toFun, SetLike.mem_coe, PrimeSpectrum.mem_basicOpen]
    intro rid; dsimp at rid
    rcases MemCarrier.clear_denominator _ rid with ‚ü®c, N, acd, eq1‚ü©
    rw [Algebra.smul_def] at eq1
    change Localization.mk (f ^ N) 1 * Localization.mk _ _ = Localization.mk _ _ at eq1
    rw [mk_mul, one_mul, mk_eq_mk', IsLocalization.eq] at eq1
    rcases eq1 with ‚ü®‚ü®_, ‚ü®M, rfl‚ü©‚ü©, eq1‚ü©
    rw [Submonoid.coe_one, one_mul] at eq1
    simp only [Subtype.coe_mk] at eq1
    have : a * f ^ N * f ^ M ‚àà y.val.asHomogeneousIdeal.toIdeal := by
      rw [mul_comm _ (f ^ N), mul_comm _ (f ^ M), eq1]
      refine' mul_mem_left _ _ (mul_mem_left _ _ (sum_mem _ fun i _ => mul_mem_left _ _ _))
      generalize_proofs h‚ÇÅ h‚ÇÇ; exact (Classical.choose_spec h‚ÇÇ).1
    rcases y.1.isPrime.mem_or_mem this with (H1 | H3)
    rcases y.1.isPrime.mem_or_mem H1 with (H1 | H2)
    ¬∑ exact hy2 H1
    ¬∑ exact y.2 (y.1.isPrime.mem_of_pow_mem N H2)
    ¬∑ exact y.2 (y.1.isPrime.mem_of_pow_mem M H3)
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec.preimage_eq AlgebraicGeometry.ProjIsoSpecTopComponent.ToSpec.preimage_eq

end ToSpec

section

variable {ùíú}

/-- The continuous function between the basic open set `D(f)` in `Proj` to the corresponding basic
open set in `Spec A‚Å∞_f`.
-/
def toSpec {f : A} : (Proj.T| pbo f) ‚ü∂ Spec.T A‚Å∞_ f where
  toFun := ToSpec.toFun f
  continuous_toFun := by
    rw [PrimeSpectrum.isTopologicalBasis_basic_opens.continuous_iff]
    rintro _ ‚ü®‚ü®k, ‚ü®a, ha‚ü©, ‚ü®b, hb1‚ü©, ‚ü®k', hb2‚ü©‚ü©, rfl‚ü©; dsimp
    erw [ToSpec.preimage_eq f a b k ha hb1 ‚ü®k', hb2‚ü©]
    refine' isOpen_induced_iff.mpr ‚ü®(pbo f).1 ‚äì (pbo a).1, IsOpen.inter (pbo f).2 (pbo a).2, _‚ü©
    ext z; constructor
    ¬∑ intro hz; simpa [Set.mem_preimage]
    ¬∑ intro hz; simpa only [Set.inf_eq_inter,Set.mem_inter_iff,Set.mem_preimage]
#align algebraic_geometry.Proj_iso_Spec_Top_component.to_Spec AlgebraicGeometry.ProjIsoSpecTopComponent.toSpec

end

namespace FromSpec

open GradedAlgebra SetLike

open Finset hiding mk_zero

-- Porting note: _root_ doesn't work here
open HomogeneousLocalization

variable {ùíú} {f : A} {m : ‚Ñï} (f_deg : f ‚àà ùíú m)

open Lean Meta Elab Tactic

macro "mem_tac_aux" : tactic =>
  `(tactic| first | exact pow_mem_graded _ (Submodule.coe_mem _) | exact nat_cast_mem_graded _ _ |
    exact pow_mem_graded _ f_deg)

macro "mem_tac" : tactic =>
  `(tactic| first | mem_tac_aux |
    repeat (all_goals (apply SetLike.GradedMonoid.toGradedMul.mul_mem)); mem_tac_aux)

/-- The function from `Spec A‚Å∞_f` to `Proj|D(f)` is defined by `q ‚Ü¶ {a | a·µ¢·µê/f‚Å± ‚àà q}`, i.e. sending
`q` a prime ideal in `A‚Å∞_f` to the homogeneous prime relevant ideal containing only and all the
elements `a : A` such that for every `i`, the degree 0 element formed by dividing the `m`-th power
of the `i`-th projection of `a` by the `i`-th power of the degree-`m` homogeneous element `f`,
lies in `q`.

The set `{a | a·µ¢·µê/f‚Å± ‚àà q}`
* is an ideal, as proved in `carrier.asIdeal`;
* is homogeneous, as proved in `carrier.asHomogeneousIdeal`;
* is prime, as proved in `carrier.asIdeal.prime`;
* is relevant, as proved in `carrier.relevant`.
-/
def carrier (q : Spec.T A‚Å∞_ f) : Set A :=
  {a | ‚àÄ i, (Quotient.mk'' ‚ü®m * i, ‚ü®proj ùíú i a ^ m, by mem_tac‚ü©,
              ‚ü®f ^ i, by rw [mul_comm]; mem_tac‚ü©, ‚ü®_, rfl‚ü©‚ü© : A‚Å∞_ f) ‚àà q.1}
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier

theorem mem_carrier_iff (q : Spec.T A‚Å∞_ f) (a : A) :
    a ‚àà carrier f_deg q ‚Üî ‚àÄ i, (Quotient.mk'' ‚ü®m * i, ‚ü®proj ùíú i a ^ m, by mem_tac‚ü©,
      ‚ü®f ^ i, by rw [mul_comm]; mem_tac‚ü©, ‚ü®_, rfl‚ü©‚ü© : A‚Å∞_ f) ‚àà q.1 :=
  Iff.rfl
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.mem_carrier_iff AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.mem_carrier_iff

theorem mem_carrier_iff' (q : Spec.T A‚Å∞_ f) (a : A) :
    a ‚àà carrier f_deg q ‚Üî
      ‚àÄ i, (Localization.mk (proj ùíú i a ^ m) ‚ü®f ^ i, ‚ü®i, rfl‚ü©‚ü© : Localization.Away f) ‚àà
          algebraMap (HomogeneousLocalization.Away ùíú f) (Localization.Away f) '' { s | s ‚àà q.1 } :=
  (mem_carrier_iff f_deg q a).trans
    (by
      constructor <;> intro h i <;> specialize h i
      ¬∑ rw [Set.mem_image]; refine' ‚ü®_, h, rfl‚ü©
      ¬∑ rw [Set.mem_image] at h; rcases h with ‚ü®x, h, hx‚ü©
        change x ‚àà q.asIdeal at h
        convert h
        rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'']
        dsimp only [Subtype.coe_mk]; rw [‚Üê hx]; rfl)
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.mem_carrier_iff' AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.mem_carrier_iff'

theorem carrier.add_mem (q : Spec.T A‚Å∞_ f) {a b : A} (ha : a ‚àà carrier f_deg q)
    (hb : b ‚àà carrier f_deg q) : a + b ‚àà carrier f_deg q := by
  refine' fun i => (q.2.mem_or_mem _).elim id id
  change (Quotient.mk'' ‚ü®_, _, _, _‚ü© : A‚Å∞_ f) ‚àà q.1; dsimp only [Subtype.coe_mk]
  simp_rw [‚Üê pow_add, map_add, add_pow, mul_comm, ‚Üê nsmul_eq_mul]
  let g : ‚Ñï ‚Üí A‚Å∞_ f := fun j => (m + m).choose j ‚Ä¢
      if h2 : m + m < j then (0 : A‚Å∞_ f)
      else
        -- Porting note: inlining `l`, `r` causes a "can't synth HMul A‚Å∞_ f A‚Å∞_ f ?" error
        if h1 : j ‚â§ m then
          letI l : A‚Å∞_ f := Quotient.mk''
            ‚ü®m * i, ‚ü®proj ùíú i a ^ j * proj ùíú i b ^ (m - j), ?_‚ü©,
              ‚ü®_, by rw [mul_comm]; mem_tac‚ü©, ‚ü®i, rfl‚ü©‚ü©
          letI r : A‚Å∞_ f := Quotient.mk''
            ‚ü®m * i, ‚ü®proj ùíú i b ^ m, by mem_tac‚ü©,
              ‚ü®_, by rw [mul_comm]; mem_tac‚ü©, ‚ü®i, rfl‚ü©‚ü©
          l * r
        else
          letI l : A‚Å∞_ f := Quotient.mk''
            ‚ü®m * i, ‚ü®proj ùíú i a ^ m, by mem_tac‚ü©,
              ‚ü®_, by rw [mul_comm]; mem_tac‚ü©, ‚ü®i, rfl‚ü©‚ü©
          letI r : A‚Å∞_ f := Quotient.mk''
            ‚ü®m * i, ‚ü®proj ùíú i a ^ (j - m) * proj ùíú i b ^ (m + m - j), ?_‚ü©,
              ‚ü®_, by rw [mul_comm]; mem_tac‚ü©, ‚ü®i, rfl‚ü©‚ü©
          l * r
  rotate_left
  ¬∑ rw [(_ : m * i = _)]
    -- Porting note: it seems unification with mul_mem is more fiddly reducing value of mem_tac
    apply GradedMonoid.toGradedMul.mul_mem (i := j ‚Ä¢ i) (j := (m - j) ‚Ä¢ i) <;> mem_tac_aux
    rw [‚Üê add_smul, Nat.add_sub_of_le h1]; rfl
  ¬∑ rw [(_ : m * i = _)]
    apply GradedMonoid.toGradedMul.mul_mem (i := (j-m) ‚Ä¢ i) (j := (m + m - j) ‚Ä¢ i) <;> mem_tac_aux
    rw [‚Üê add_smul]; congr; zify [le_of_not_lt h2, le_of_not_le h1]; abel
  convert_to ‚àë i in range (m + m + 1), g i ‚àà q.1; swap
  ¬∑ refine' q.1.sum_mem fun j _ => nsmul_mem _ _; split_ifs
    exacts [q.1.zero_mem, q.1.mul_mem_left _ (hb i), q.1.mul_mem_right _ (ha i)]
  rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'']
  change _ = (algebraMap (HomogeneousLocalization.Away ùíú f) (Localization.Away f)) _
  dsimp only [Subtype.coe_mk]; rw [map_sum, mk_sum]
  apply Finset.sum_congr rfl fun j hj => _
  intro j hj
  change _ = HomogeneousLocalization.val _
  rw [HomogeneousLocalization.smul_val]
  split_ifs with h2 h1
  ¬∑ exact ((Finset.mem_range.1 hj).not_le h2).elim
  all_goals simp only [HomogeneousLocalization.mul_val, HomogeneousLocalization.zero_val,
    HomogeneousLocalization.val_mk'', Subtype.coe_mk, mk_mul, ‚Üê smul_mk]; congr 2
  ¬∑ dsimp; rw [mul_assoc, ‚Üê pow_add, add_comm (m - j), Nat.add_sub_assoc h1]
  ¬∑ simp_rw [pow_add]; rfl
  ¬∑ dsimp; rw [‚Üê mul_assoc, ‚Üê pow_add, Nat.add_sub_of_le (le_of_not_le h1)]
  ¬∑ simp_rw [pow_add]; rfl
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.add_mem AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.add_mem

variable (hm : 0 < m) (q : Spec.T A‚Å∞_ f)

theorem carrier.zero_mem : (0 : A) ‚àà carrier f_deg q := fun i => by
  convert Submodule.zero_mem q.1 using 1
  rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
    HomogeneousLocalization.zero_val]; simp_rw [map_zero, zero_pow hm]
  convert Localization.mk_zero (S := Submonoid.powers f) _ using 1
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.zero_mem AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.zero_mem

theorem carrier.smul_mem (c x : A) (hx : x ‚àà carrier f_deg q) : c ‚Ä¢ x ‚àà carrier f_deg q := by
  revert c
  refine' DirectSum.Decomposition.inductionOn ùíú _ _ _
  ¬∑ rw [zero_smul]; exact carrier.zero_mem f_deg hm _
  ¬∑ rintro n ‚ü®a, ha‚ü© i
    simp_rw [proj_apply, smul_eq_mul, coe_decompose_mul_of_left_mem ùíú i ha]
    -- Porting note: having trouble with Mul instance
    let product : A‚Å∞_ f :=
      Mul.mul (Quotient.mk'' ‚ü®_, ‚ü®a ^ m, pow_mem_graded m ha‚ü©, ‚ü®_, ?_‚ü©, ‚ü®n, rfl‚ü©‚ü© : A‚Å∞_ f)
          (Quotient.mk'' ‚ü®_, ‚ü®proj ùíú (i - n) x ^ m, by mem_tac‚ü©, ‚ü®_, ?_‚ü©, ‚ü®i - n, rfl‚ü©‚ü© : A‚Å∞_ f)
    split_ifs with h
    ¬∑ convert_to product ‚àà q.1
      ¬∑ dsimp
        erw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
          HomogeneousLocalization.mul_val, HomogeneousLocalization.val_mk'',
          HomogeneousLocalization.val_mk'']
        simp_rw [mul_pow]; rw [Localization.mk_mul]
        congr; erw [‚Üê pow_add, Nat.add_sub_of_le h]
        ¬∑ rw [(_ : m ‚Ä¢ n = _)]; mem_tac; simp only [smul_eq_mul, mul_comm]
        ¬∑ rw [(_ : m ‚Ä¢ (i - n) = _)]; mem_tac; simp only [smul_eq_mul, mul_comm]
      ¬∑ apply Ideal.mul_mem_left (Œ± := A‚Å∞_ f) _ _ (hx _)
    ¬∑ simp_rw [zero_pow hm]; convert carrier.zero_mem f_deg hm q i; rw [map_zero, zero_pow hm]
  ¬∑ simp_rw [add_smul]; exact fun _ _ => carrier.add_mem f_deg q
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.smul_mem AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.smul_mem

/-- For a prime ideal `q` in `A‚Å∞_f`, the set `{a | a·µ¢·µê/f‚Å± ‚àà q}` as an ideal.
-/
def carrier.asIdeal : Ideal A where
  carrier := carrier f_deg q
  zero_mem' := carrier.zero_mem f_deg hm q
  add_mem' := carrier.add_mem f_deg q
  smul_mem' := carrier.smul_mem f_deg hm q
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.as_ideal AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal


theorem carrier.asIdeal.homogeneous : (carrier.asIdeal f_deg hm q).IsHomogeneous ùíú :=
  fun i a ha j =>
  (em (i = j)).elim (fun h => h ‚ñ∏ by simpa only [proj_apply, decompose_coe, of_eq_same] using ha _)
    fun h => by
    simp only [proj_apply, decompose_of_mem_ne ùíú (Submodule.coe_mem (decompose ùíú a i)) h,
      zero_pow hm]
    convert carrier.zero_mem f_deg hm q j; rw [map_zero, zero_pow hm]
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.as_ideal.homogeneous AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal.homogeneous

/-- For a prime ideal `q` in `A‚Å∞_f`, the set `{a | a·µ¢·µê/f‚Å± ‚àà q}` as a homogeneous ideal.
-/
def carrier.asHomogeneousIdeal : HomogeneousIdeal ùíú :=
  ‚ü®carrier.asIdeal f_deg hm q, carrier.asIdeal.homogeneous f_deg hm q‚ü©
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.as_homogeneous_ideal AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.asHomogeneousIdeal

theorem carrier.denom_not_mem : f ‚àâ carrier.asIdeal f_deg hm q := fun rid =>
  q.IsPrime.ne_top <|
    (Ideal.eq_top_iff_one _).mpr
      (by
        convert rid m
        rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.one_val,
          HomogeneousLocalization.val_mk'']
        dsimp
        simp_rw [decompose_of_mem_same _ f_deg]
        simp only [mk_eq_monoidOf_mk', Submonoid.LocalizationMap.mk'_self])
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.denom_not_mem AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.denom_not_mem

theorem carrier.relevant : ¬¨HomogeneousIdeal.irrelevant ùíú ‚â§ carrier.asHomogeneousIdeal f_deg hm q :=
  fun rid => carrier.denom_not_mem f_deg hm q <| rid <| DirectSum.decompose_of_mem_ne ùíú f_deg hm.ne'
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.relevant AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.relevant

theorem carrier.asIdeal.ne_top : carrier.asIdeal f_deg hm q ‚â† ‚ä§ := fun rid =>
  carrier.denom_not_mem f_deg hm q (rid.symm ‚ñ∏ Submodule.mem_top)
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.as_ideal.ne_top AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal.ne_top

theorem carrier.asIdeal.prime : (carrier.asIdeal f_deg hm q).IsPrime :=
  (carrier.asIdeal.homogeneous f_deg hm q).isPrime_of_homogeneous_mem_or_mem
    (carrier.asIdeal.ne_top f_deg hm q) fun {x y} ‚ü®nx, hnx‚ü© ‚ü®ny, hny‚ü© hxy =>
    show (‚àÄ i, _ ‚àà _) ‚à® ‚àÄ i, _ ‚àà _ by
      rw [‚Üê and_forall_ne nx, and_iff_left, ‚Üê and_forall_ne ny, and_iff_left]
      ¬∑ apply q.2.mem_or_mem; convert hxy (nx + ny) using 1
        dsimp
        simp_rw [decompose_of_mem_same ùíú hnx, decompose_of_mem_same ùíú hny,
          decompose_of_mem_same ùíú (SetLike.GradedMonoid.toGradedMul.mul_mem hnx hny),
          mul_pow, pow_add]
        simp only [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
          HomogeneousLocalization.mul_val, mk_mul]
        simp only [Submonoid.mk_mul_mk, mk_eq_monoidOf_mk']
      all_goals
        intro n hn; convert q.1.zero_mem using 1
        rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
          HomogeneousLocalization.zero_val]; simp_rw [proj_apply]
        convert mk_zero (S := Submonoid.powers f) _
        rw [decompose_of_mem_ne ùíú _ hn.symm, zero_pow hm]
        ¬∑ first | exact hnx | exact hny
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.carrier.as_ideal.prime AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.carrier.asIdeal.prime

/-- The function `Spec A‚Å∞_f ‚Üí Proj|D(f)` by sending `q` to `{a | a·µ¢·µê/f‚Å± ‚àà q}`.
-/
def toFun : (Spec.T A‚Å∞_ f) ‚Üí Proj.T| pbo f := fun q =>
  ‚ü®‚ü®carrier.asHomogeneousIdeal f_deg hm q, carrier.asIdeal.prime f_deg hm q,
      carrier.relevant f_deg hm q‚ü©,
    (ProjectiveSpectrum.mem_basicOpen _ f _).mp <| carrier.denom_not_mem f_deg hm q‚ü©
#align algebraic_geometry.Proj_iso_Spec_Top_component.from_Spec.to_fun AlgebraicGeometry.ProjIsoSpecTopComponent.FromSpec.toFun

end FromSpec

section toSpecFromSpec

lemma toSpecFromSpec {f : A} {m : ‚Ñï} (hm : 0 < m) (f_deg : f ‚àà ùíú m) (x : Spec.T (A‚Å∞_ f)) :
    toSpec (FromSpec.toFun f_deg hm x) = x := show _ = (_ : PrimeSpectrum _) by
  ext (z : A‚Å∞_ f); fconstructor <;> intro hz
  ¬∑ change z ‚àà ToSpec.carrier _ at hz
    erw [ToSpec.carrier_eq_span, mem_span_set] at hz
    obtain ‚ü®c, support_le, rfl‚ü© := hz
    refine Ideal.sum_mem _ fun j hj ‚Ü¶ Ideal.mul_mem_left _ _ ?_
    obtain ‚ü®s, _, hs, n, s_mem, F_mem, ‚ü®k, rfl‚ü©, rfl‚ü© := support_le hj
    refine x.IsPrime.mem_of_pow_mem m <| show Quotient.mk'' ‚ü®_, ‚ü®s^m, _‚ü©, _, _‚ü© ‚àà x.asIdeal from ?_
    -- simp only [CommRingCat.coe_of, smul_eq_mul]
    convert hs n using 1
    rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
      HomogeneousLocalization.val_mk'']
    simp only [smul_eq_mul, SetLike.coe_gnpow, GradedAlgebra.proj_apply,
      DirectSum.decompose_of_mem_same ùíú s_mem]
    by_cases ineq : f^k = 0
    ¬∑ have := IsLocalization.uniqueOfZeroMem (M := Submonoid.powers f) (S := Localization.Away f)
        ‚ü®k, ineq‚ü©
      exact Subsingleton.elim _ _
    ¬∑ simp_rw [‚Üê pow_mul]; congr
      exact DirectSum.degree_eq_of_mem_mem ùíú F_mem (SetLike.pow_mem_graded k f_deg) ineq |>.symm

  ¬∑ erw [ToSpec.mem_carrier_iff]
    obtain ‚ü®k, (k_spec : f^k = z.den)‚ü© := z.den_mem
    rw [show z.val = (Localization.mk z.num ‚ü®f^k, ‚ü®k, rfl‚ü©‚ü© : Away f) by
        ¬∑ rw [z.eq_num_div_den]; congr; exact k_spec.symm,
      show (mk z.num ‚ü®f^k, ‚ü®k, rfl‚ü©‚ü© : Away f) = mk z.num 1 * (mk 1 ‚ü®f^k, ‚ü®k, rfl‚ü©‚ü© : Away f) by
        ¬∑ rw [mk_mul, mul_one, one_mul]]
    refine Ideal.mul_mem_right _ _ <| Ideal.subset_span ‚ü®z.num, ?_, rfl‚ü©

    intro j
    by_cases ineq : z.deg = j
    ¬∑ subst ineq
      simp only [CommRingCat.coe_of, GradedAlgebra.proj_apply,
        DirectSum.decompose_of_mem_same ùíú z.num_mem_deg]
      convert x.IsPrime.pow_mem_iff_mem m hm |>.mpr hz using 1
      rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
        HomogeneousLocalization.pow_val, z.eq_num_div_den, Localization.mk_pow]
      by_cases ineq : f^k = 0
      ¬∑ have := IsLocalization.uniqueOfZeroMem (M := Submonoid.powers f) (S := Localization.Away f)
          ‚ü®k, ineq‚ü©
        exact Subsingleton.elim _ _
      ¬∑ dsimp; congr 2
        rw [‚Üê k_spec, ‚Üê pow_mul, show z.deg = k * m from
          degree_eq_of_mem_mem ùíú (k_spec ‚ñ∏ z.den_mem_deg) (SetLike.pow_mem_graded k f_deg) ineq]
    ¬∑ simp only [CommRingCat.coe_of, GradedAlgebra.proj_apply, zero_pow hm,
        DirectSum.decompose_of_mem_ne ùíú z.num_mem_deg ineq]
      convert x.asIdeal.zero_mem
      rw [HomogeneousLocalization.ext_iff_val, HomogeneousLocalization.val_mk'',
        HomogeneousLocalization.zero_val, Localization.mk_zero]

end toSpecFromSpec

section fromSpecToSpec

set_option maxHeartbeats 400000 in
open GradedAlgebra in
lemma fromSpecToSpec {f : A} {m : ‚Ñï} (hm : 0 < m) (f_deg : f ‚àà ùíú m) (x : Proj.T| pbo f) :
    FromSpec.toFun f_deg hm (toSpec x) = x := by
  classical
  refine Subtype.ext <| ProjectiveSpectrum.ext _ _ <| HomogeneousIdeal.ext <| Ideal.ext fun z ‚Ü¶ ?_
  fconstructor <;> intro hz
  ¬∑ rw [‚Üê DirectSum.sum_support_decompose ùíú z]
    refine Ideal.sum_mem _ fun i _ ‚Ü¶ ?_
    specialize hz i
    erw [ToSpec.mem_carrier_iff, HomogeneousLocalization.val_mk'', mem_span_set] at hz
    obtain ‚ü®c, support_le, (eq1 : Finset.sum _ _ = _)‚ü© := hz
    simp only [smul_eq_mul, GradedAlgebra.proj_apply] at eq1
    obtain ‚ü®N, hN‚ü© := Localization.Away.den_one_smul_large_power (c.support.image fun i ‚Ü¶ c i * i)
    choose acd hacd using hN
    have prop1 : ‚àÄ i ‚àà c.support, c i * i ‚àà c.support.image fun i ‚Ü¶ c i * i := by aesop

    have eq2 := calc
      f^(i+N) ‚Ä¢ (mk ((decompose ùíú z i) ^ m) ‚ü®f^i, ‚ü®_, rfl‚ü©‚ü© : Away f)
      = f^(i+N) ‚Ä¢ ‚àë i in c.support, c i * i := by rw [eq1]
      _ = f^i ‚Ä¢ f^N ‚Ä¢ ‚àë i in c.support, c i * i := by rw [‚Üê mul_smul, pow_add]
      _ = f^i ‚Ä¢ ‚àë i in c.support, f^N ‚Ä¢ (c i * i) := by rw [Finset.smul_sum]
      _ = f^i ‚Ä¢ ‚àë i in c.support.attach, f^N ‚Ä¢ (c i.1 * i.1) := by
        congr 1; exact Finset.sum_attach _ _ |>.symm
      _ = f^i ‚Ä¢ ‚àë i in c.support.attach, algebraMap A (Away f) (acd _ (prop1 _ i.2)) := by
        congr 1; exact Finset.sum_congr rfl fun i _ ‚Ü¶ by rw [hacd _ (prop1 _ i.2)]
      _ = f^i ‚Ä¢ algebraMap A (Away f) (‚àë i in c.support.attach, acd _ (prop1 _ i.2)) := by
        congr 1; rw [map_sum]
      _ = algebraMap A (Away f) (f^i * ‚àë i in c.support.attach, acd _ (prop1 _ i.2)) := by
        rw [map_mul, Algebra.smul_def]

    have eq3 : f^(i+N) ‚Ä¢ (mk ((decompose ùíú z i) ^ m) ‚ü®f^i, ‚ü®_, rfl‚ü©‚ü© : Away f) =
      f^N ‚Ä¢ (mk ((decompose ùíú z i) ^ m) 1 : Away f)
    ¬∑ rw [smul_mk, smul_mk, smul_eq_mul, Localization.mk_eq_mk_iff, Localization.r_iff_exists]
      refine ‚ü®1, ?_‚ü©
      simp only [OneMemClass.coe_one, one_mul, smul_eq_mul]
      ring
    conv_rhs at eq3 => rw [mk_one_eq_algebraMap, Algebra.smul_def, ‚Üê map_mul]
    rw [eq2, ‚Üê mk_one_eq_algebraMap, ‚Üê mk_one_eq_algebraMap, Localization.mk_eq_mk_iff,
      Localization.r_iff_exists] at eq3
    obtain ‚ü®‚ü®_, ‚ü®k, rfl‚ü©‚ü©, eq3‚ü© := eq3
    simp only [OneMemClass.coe_one, one_mul, ‚Üê mul_assoc, ‚Üê pow_add] at eq3

    suffices m : f^(k + i) * ‚àë i in c.support.attach, acd _ (prop1 _ i.2) ‚àà x.1.asHomogeneousIdeal
    ¬∑ exact x.1.isPrime.mem_of_pow_mem _ <| x.1.isPrime.mem_or_mem (eq3 ‚ñ∏ m)
        |>.resolve_left fun r ‚Ü¶ ProjectiveSpectrum.mem_basicOpen ùíú _ _
        |>.mp x.2 <| x.1.isPrime.mem_of_pow_mem _ r

    refine Ideal.mul_mem_left _ _ <| Ideal.sum_mem _ fun j _ ‚Ü¶ ?_
    obtain ‚ü®k, z, eq4‚ü© : ‚àÉ (k : ‚Ñï) (z : A), c j.1 = (mk z ‚ü®f^k, ‚ü®_, rfl‚ü©‚ü© : Away f)
    ¬∑ induction' (c j) using Localization.induction_on with d
      rcases d with ‚ü®z, ‚ü®_, ‚ü®k, rfl‚ü©‚ü©‚ü©
      exact ‚ü®_, _, rfl‚ü©
    obtain ‚ü®g, hg1, hg2‚ü© := support_le j.2
    specialize hacd _ (prop1 _ j.2)
    conv_rhs at hacd => rw [mul_comm (c j.1) j.1, eq4, ‚Üê hg2, ‚Üê Algebra.smul_def, smul_mk,
      smul_mk, smul_eq_mul, smul_eq_mul]
    rw [‚Üê mk_one_eq_algebraMap, Localization.mk_eq_mk_iff, Localization.r_iff_exists] at hacd
    obtain ‚ü®‚ü®_, ‚ü®l, rfl‚ü©‚ü©, eq5‚ü© := hacd
    simp only [OneMemClass.coe_one, one_mul, ‚Üê mul_assoc, ‚Üê pow_add] at eq5
    suffices m : f^(l + N) * g * z ‚àà x.1.asHomogeneousIdeal
    ¬∑ exact x.1.isPrime.mem_or_mem (eq5.symm ‚ñ∏ m)
          |>.resolve_left fun r ‚Ü¶ ProjectiveSpectrum.mem_basicOpen ùíú _ _
          |>.mp x.2 <| x.1.isPrime.mem_of_pow_mem _ r
    exact Ideal.mul_mem_right _ _ <| Ideal.mul_mem_left _ _ hg1

  ¬∑ intro i
    erw [ToSpec.mem_carrier_iff, HomogeneousLocalization.val_mk'']
    dsimp only [proj_apply]
    rw [show (mk (decompose ùíú z i ^ m) ‚ü®f^i, ‚ü®i, rfl‚ü©‚ü©: Away f) =
      (decompose ùíú z i ^ m : A) ‚Ä¢ (mk 1 ‚ü®f^i, ‚ü®i, rfl‚ü©‚ü© : Away f) by
      ¬∑ rw [smul_mk, smul_eq_mul, mul_one], Algebra.smul_def]
    exact Ideal.mul_mem_right _ _ <|
      Ideal.subset_span ‚ü®_, ‚ü®Ideal.pow_mem_of_mem _ (x.1.asHomogeneousIdeal.2 i hz) _ hm, rfl‚ü©‚ü©

lemma toSpec_injective {f : A} {m : ‚Ñï} (hm : 0 < m) (f_deg : f ‚àà ùíú m):
    Function.Injective (toSpec (ùíú := ùíú) (f := f)) := by
  intro x‚ÇÅ x‚ÇÇ h
  have := congr_arg (FromSpec.toFun f_deg hm) h
  rwa [fromSpecToSpec, fromSpecToSpec] at this

lemma toSpec_surjective {f : A} {m : ‚Ñï} (hm : 0 < m) (f_deg : f ‚àà ùíú m):
    Function.Surjective (toSpec (ùíú := ùíú) (f := f)) :=
  Function.surjective_iff_hasRightInverse |>.mpr
    ‚ü®FromSpec.toFun f_deg hm, toSpecFromSpec ùíú hm f_deg‚ü©

lemma toSpec_bijective {f : A} {m : ‚Ñï} (hm : 0 < m) (f_deg : f ‚àà ùíú m):
    Function.Bijective (toSpec (ùíú := ùíú) (f := f)) :=
  ‚ü®toSpec_injective ùíú hm f_deg, toSpec_surjective ùíú hm f_deg‚ü©

end fromSpecToSpec

end ProjIsoSpecTopComponent

end AlgebraicGeometry
