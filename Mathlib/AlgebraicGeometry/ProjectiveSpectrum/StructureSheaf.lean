/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import Mathlib.AlgebraicGeometry.ProjectiveSpectrum.Topology
import Mathlib.Topology.Sheaves.LocalPredicate
import Mathlib.RingTheory.GradedAlgebra.HomogeneousLocalization
import Mathlib.Geometry.RingedSpace.LocallyRingedSpace

#align_import algebraic_geometry.projective_spectrum.structure_sheaf from "leanprover-community/mathlib"@"486cb2f3bda4a67557c6285f5bd0c3348c1eea81"

/-!
# The structure sheaf on `projective_spectrum ùíú`.

In `Mathlib.AlgebraicGeometry.Topology`, we have given a topology on `ProjectiveSpectrum ùíú`; in
this file we will construct a sheaf on `ProjectiveSpectrum ùíú`.

## Notation
- `R` is a commutative semiring;
- `A` is a commutative ring and an `R`-algebra;
- `ùíú : ‚Ñï ‚Üí Submodule R A` is the grading of `A`;
- `U` is opposite object of some open subset of `ProjectiveSpectrum.top`.

## Main definitions and results
We define the structure sheaf as the subsheaf of all dependent function
`f : Œ† x : U, HomogeneousLocalization ùíú x` such that `f` is locally expressible as ratio of two
elements of the *same grading*, i.e. `‚àÄ y ‚àà U, ‚àÉ (V ‚äÜ U) (i : ‚Ñï) (a b ‚àà ùíú i), ‚àÄ z ‚àà V, f z = a / b`.

* `AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.isLocallyFraction`: the predicate that
  a dependent function is locally expressible as a ratio of two elements of the same grading.
* `AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.sectionsSubring`: the dependent functions
  satisfying the above local property forms a subring of all dependent functions
  `Œ† x : U, HomogeneousLocalization ùíú x`.
* `AlgebraicGeometry.Proj.StructureSheaf`: the sheaf with `U ‚Ü¶ sectionsSubring U` and natural
  restriction map.

Then we establish that `Proj ùíú` is a `LocallyRingedSpace`:
* `AlgebraicGeometry.Proj.stalkIso'`: for any `x : ProjectiveSpectrum ùíú`, the stalk of
  `Proj.StructureSheaf` at `x` is isomorphic to `HomogeneousLocalization ùíú x`.
* `AlgebraicGeometry.Proj.toLocallyRingedSpace`: `Proj` as a locally ringed space.

## References

* [Robin Hartshorne, *Algebraic Geometry*][Har77]


-/


noncomputable section

namespace AlgebraicGeometry

open scoped DirectSum BigOperators Pointwise

open DirectSum SetLike Localization TopCat TopologicalSpace CategoryTheory Opposite

variable {R A : Type*}

variable [CommRing R] [CommRing A] [Algebra R A]

variable (ùíú : ‚Ñï ‚Üí Submodule R A) [GradedAlgebra ùíú]

local notation3 "at " x =>
  HomogeneousLocalization.AtPrime ùíú
    (HomogeneousIdeal.toIdeal (ProjectiveSpectrum.asHomogeneousIdeal x))

namespace ProjectiveSpectrum.StructureSheaf

variable {ùíú}

/-- The predicate saying that a dependent function on an open `U` is realised as a fixed fraction
`r / s` of *same grading* in each of the stalks (which are localizations at various prime ideals).
-/
def IsFraction {U : Opens (ProjectiveSpectrum.top ùíú)} (f : ‚àÄ x : U, at x.1) : Prop :=
  ‚àÉ (i : ‚Ñï) (r s : ùíú i),
    ‚àÄ x : U, ‚àÉ s_nin : s.1 ‚àâ x.1.asHomogeneousIdeal, f x = Quotient.mk'' ‚ü®i, r, s, s_nin‚ü©
#align algebraic_geometry.projective_spectrum.structure_sheaf.is_fraction AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.IsFraction
variable (ùíú)

/--
The predicate `IsFraction` is "prelocal", in the sense that if it holds on `U` it holds on any open
subset `V` of `U`.
-/
def isFractionPrelocal : PrelocalPredicate fun x : ProjectiveSpectrum.top ùíú => at x where
  pred f := IsFraction f
  res := by rintro V U i f ‚ü®j, r, s, w‚ü©; exact ‚ü®j, r, s, fun y => w (i y)‚ü©
#align algebraic_geometry.projective_spectrum.structure_sheaf.is_fraction_prelocal AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.isFractionPrelocal

/-- We will define the structure sheaf as the subsheaf of all dependent functions in
`Œ† x : U, HomogeneousLocalization ùíú x` consisting of those functions which can locally be expressed
as a ratio of `A` of same grading.-/
def isLocallyFraction : LocalPredicate fun x : ProjectiveSpectrum.top ùíú => at x :=
  (isFractionPrelocal ùíú).sheafify
#align algebraic_geometry.projective_spectrum.structure_sheaf.is_locally_fraction AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.isLocallyFraction

namespace SectionSubring

variable {ùíú}

open Submodule SetLike.GradedMonoid HomogeneousLocalization

theorem zeroMem' (U : (Opens (ProjectiveSpectrum.top ùíú))·µí·µñ) :
    (isLocallyFraction ùíú).pred (0 : ‚àÄ x : U.unop, at x.1) := fun x =>
  ‚ü®unop U, x.2, ùüô (unop U), ‚ü®0, ‚ü®0, zero_mem _‚ü©, ‚ü®1, one_mem_graded _‚ü©, fun _ => ‚ü®_, rfl‚ü©‚ü©‚ü©
#align algebraic_geometry.projective_spectrum.structure_sheaf.section_subring.zero_mem' AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.SectionSubring.zeroMem'

theorem oneMem' (U : (Opens (ProjectiveSpectrum.top ùíú))·µí·µñ) :
    (isLocallyFraction ùíú).pred (1 : ‚àÄ x : U.unop, at x.1) := fun x =>
  ‚ü®unop U, x.2, ùüô (unop U), ‚ü®0, ‚ü®1, one_mem_graded _‚ü©, ‚ü®1, one_mem_graded _‚ü©, fun _ => ‚ü®_, rfl‚ü©‚ü©‚ü©
#align algebraic_geometry.projective_spectrum.structure_sheaf.section_subring.one_mem' AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.SectionSubring.oneMem'

theorem addMem' (U : (Opens (ProjectiveSpectrum.top ùíú))·µí·µñ) (a b : ‚àÄ x : U.unop, at x.1)
    (ha : (isLocallyFraction ùíú).pred a) (hb : (isLocallyFraction ùíú).pred b) :
    (isLocallyFraction ùíú).pred (a + b) := fun x => by
  rcases ha x with ‚ü®Va, ma, ia, ja, ‚ü®ra, ra_mem‚ü©, ‚ü®sa, sa_mem‚ü©, wa‚ü©
  rcases hb x with ‚ü®Vb, mb, ib, jb, ‚ü®rb, rb_mem‚ü©, ‚ü®sb, sb_mem‚ü©, wb‚ü©
  refine'
    ‚ü®Va ‚äì Vb, ‚ü®ma, mb‚ü©, Opens.infLELeft _ _ ‚â´ ia, ja + jb,
      ‚ü®sb * ra + sa * rb,
        add_mem (add_comm jb ja ‚ñ∏ mul_mem_graded sb_mem ra_mem : sb * ra ‚àà ùíú (ja + jb))
          (mul_mem_graded sa_mem rb_mem)‚ü©,
      ‚ü®sa * sb, mul_mem_graded sa_mem sb_mem‚ü©, fun y => ‚ü®fun h => _, _‚ü©‚ü©
  ¬∑ cases' (y : ProjectiveSpectrum.top ùíú).isPrime.mem_or_mem h with h h
    ¬∑ obtain ‚ü®nin, -‚ü© := wa ‚ü®y, (Opens.infLELeft Va Vb y).2‚ü©; exact nin h
    ¬∑ obtain ‚ü®nin, -‚ü© := wb ‚ü®y, (Opens.infLERight Va Vb y).2‚ü©; exact nin h
  ¬∑ simp only [add_mul, map_add, Pi.add_apply, RingHom.map_mul, ext_iff_val, add_val]
    obtain ‚ü®nin1, hy1‚ü© := wa (Opens.infLELeft Va Vb y)
    obtain ‚ü®nin2, hy2‚ü© := wb (Opens.infLERight Va Vb y)
    dsimp only at hy1 hy2
    erw [hy1, hy2]
    simp only [val_mk'', add_mk, add_comm (sa * rb)]
    rfl
#align algebraic_geometry.projective_spectrum.structure_sheaf.section_subring.add_mem' AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.SectionSubring.addMem'

theorem negMem' (U : (Opens (ProjectiveSpectrum.top ùíú))·µí·µñ) (a : ‚àÄ x : U.unop, at x.1)
    (ha : (isLocallyFraction ùíú).pred a) : (isLocallyFraction ùíú).pred (-a) := fun x => by
  rcases ha x with ‚ü®V, m, i, j, ‚ü®r, r_mem‚ü©, ‚ü®s, s_mem‚ü©, w‚ü©
  choose nin hy using w
  refine' ‚ü®V, m, i, j, ‚ü®-r, Submodule.neg_mem _ r_mem‚ü©, ‚ü®s, s_mem‚ü©, fun y => ‚ü®nin y, _‚ü©‚ü©
  simp only [ext_iff_val, val_mk''] at hy
  simp only [Pi.neg_apply, ext_iff_val, neg_val, hy, val_mk'', neg_mk]
#align algebraic_geometry.projective_spectrum.structure_sheaf.section_subring.neg_mem' AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.SectionSubring.negMem'

theorem mulMem' (U : (Opens (ProjectiveSpectrum.top ùíú))·µí·µñ) (a b : ‚àÄ x : U.unop, at x.1)
    (ha : (isLocallyFraction ùíú).pred a) (hb : (isLocallyFraction ùíú).pred b) :
    (isLocallyFraction ùíú).pred (a * b) := fun x => by
  rcases ha x with ‚ü®Va, ma, ia, ja, ‚ü®ra, ra_mem‚ü©, ‚ü®sa, sa_mem‚ü©, wa‚ü©
  rcases hb x with ‚ü®Vb, mb, ib, jb, ‚ü®rb, rb_mem‚ü©, ‚ü®sb, sb_mem‚ü©, wb‚ü©
  refine'
    ‚ü®Va ‚äì Vb, ‚ü®ma, mb‚ü©, Opens.infLELeft _ _ ‚â´ ia, ja + jb,
      ‚ü®ra * rb, SetLike.mul_mem_graded ra_mem rb_mem‚ü©,
      ‚ü®sa * sb, SetLike.mul_mem_graded sa_mem sb_mem‚ü©, fun y => ‚ü®fun h => _, _‚ü©‚ü©
  ¬∑ cases' (y : ProjectiveSpectrum.top ùíú).isPrime.mem_or_mem h with h h
    ¬∑ choose nin _ using wa ‚ü®y, (Opens.infLELeft Va Vb y).2‚ü©; exact nin h
    ¬∑ choose nin _ using wb ‚ü®y, (Opens.infLERight Va Vb y).2‚ü©; exact nin h
  ¬∑ simp only [Pi.mul_apply, RingHom.map_mul]
    choose nin1 hy1 using wa (Opens.infLELeft Va Vb y)
    choose nin2 hy2 using wb (Opens.infLERight Va Vb y)
    rw [ext_iff_val] at hy1 hy2 ‚ä¢
    erw [mul_val, hy1, hy2]
    simp only [val_mk'', mk_mul]
    rfl
#align algebraic_geometry.projective_spectrum.structure_sheaf.section_subring.mul_mem' AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.SectionSubring.mulMem'

end SectionSubring

section

open SectionSubring

variable {ùíú}

/-- The functions satisfying `isLocallyFraction` form a subring of all dependent functions
`Œ† x : U, HomogeneousLocalization ùíú x`.-/
def sectionsSubring (U : (Opens (ProjectiveSpectrum.top ùíú))·µí·µñ) : Subring (‚àÄ x : U.unop, at x.1)
    where
  carrier := {f | (isLocallyFraction ùíú).pred f}
  zero_mem' := zeroMem' U
  one_mem' := oneMem' U
  add_mem' := addMem' U _ _
  neg_mem' := negMem' U _
  mul_mem' := mulMem' U _ _
#align algebraic_geometry.projective_spectrum.structure_sheaf.sections_subring AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.sectionsSubring

end

/-- The structure sheaf (valued in `Type`, not yet `CommRing`) is the subsheaf consisting of
functions satisfying `isLocallyFraction`.-/
def structureSheafInType : Sheaf (Type _) (ProjectiveSpectrum.top ùíú) :=
  subsheafToTypes (isLocallyFraction ùíú)
#align algebraic_geometry.projective_spectrum.structure_sheaf.structure_sheaf_in_Type AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.structureSheafInType

instance commRingStructureSheafInTypeObj (U : (Opens (ProjectiveSpectrum.top ùíú))·µí·µñ) :
    CommRing ((structureSheafInType ùíú).1.obj U) :=
  (sectionsSubring U).toCommRing
#align algebraic_geometry.projective_spectrum.structure_sheaf.comm_ring_structure_sheaf_in_Type_obj AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.commRingStructureSheafInTypeObj

/-- The structure presheaf, valued in `CommRing`, constructed by dressing up the `Type` valued
structure presheaf.-/
@[simps]
def structurePresheafInCommRing : Presheaf CommRingCat (ProjectiveSpectrum.top ùíú) where
  obj U := CommRingCat.of ((structureSheafInType ùíú).1.obj U)
  map i :=
    { toFun := (structureSheafInType ùíú).1.map i
      map_zero' := rfl
      map_add' := fun x y => rfl
      map_one' := rfl
      map_mul' := fun x y => rfl }
set_option linter.uppercaseLean3 false in
#align algebraic_geometry.projective_spectrum.structure_sheaf.structure_presheaf_in_CommRing AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.structurePresheafInCommRing

/-- Some glue, verifying that that structure presheaf valued in `CommRing` agrees with the `Type`
valued structure presheaf.-/
def structurePresheafCompForget :
    structurePresheafInCommRing ùíú ‚ãô forget CommRingCat ‚âÖ (structureSheafInType ùíú).1 :=
  NatIso.ofComponents (fun U => Iso.refl _) (by aesop_cat)
#align algebraic_geometry.projective_spectrum.structure_sheaf.structure_presheaf_comp_forget AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.structurePresheafCompForget

end ProjectiveSpectrum.StructureSheaf

namespace ProjectiveSpectrum

open TopCat.Presheaf ProjectiveSpectrum.StructureSheaf Opens

/-- The structure sheaf on `Proj` ùíú, valued in `CommRing`.-/
def Proj.structureSheaf : Sheaf CommRingCat (ProjectiveSpectrum.top ùíú) :=
  ‚ü®structurePresheafInCommRing ùíú,
    (-- We check the sheaf condition under `forget CommRing`.
          isSheaf_iff_isSheaf_comp
          _ _).mpr
      (isSheaf_of_iso (structurePresheafCompForget ùíú).symm (structureSheafInType ùíú).cond)‚ü©
set_option linter.uppercaseLean3 false in
#align algebraic_geometry.projective_spectrum.Proj.structure_sheaf AlgebraicGeometry.ProjectiveSpectrum.Proj.structureSheaf

end ProjectiveSpectrum

section

open ProjectiveSpectrum ProjectiveSpectrum.StructureSheaf Opens

@[simp]
theorem res_apply (U V : Opens (ProjectiveSpectrum.top ùíú)) (i : V ‚ü∂ U)
    (s : (Proj.structureSheaf ùíú).1.obj (op U)) (x : V) :
    ((Proj.structureSheaf ùíú).1.map i.op s).1 x = (s.1 (i x) : _) :=
  rfl
#align algebraic_geometry.res_apply AlgebraicGeometry.res_apply

/-- `Proj` of a graded ring as a `SheafedSpace`-/
def Proj.toSheafedSpace : SheafedSpace CommRingCat where
  carrier := TopCat.of (ProjectiveSpectrum ùíú)
  presheaf := (Proj.structureSheaf ùíú).1
  IsSheaf := (Proj.structureSheaf ùíú).2
set_option linter.uppercaseLean3 false in
#align algebraic_geometry.Proj.to_SheafedSpace AlgebraicGeometry.Proj.toSheafedSpace

/-- The ring homomorphism that takes a section of the structure sheaf of `Proj` on the open set `U`,
implemented as a subtype of dependent functions to localizations at homogeneous prime ideals, and
evaluates the section on the point corresponding to a given homogeneous prime ideal. -/
def openToLocalization (U : Opens (ProjectiveSpectrum.top ùíú)) (x : ProjectiveSpectrum.top ùíú)
    (hx : x ‚àà U) : (Proj.structureSheaf ùíú).1.obj (op U) ‚ü∂ CommRingCat.of (at x) where
  toFun s := (s.1 ‚ü®x, hx‚ü© : _)
  map_one' := rfl
  map_mul' _ _ := rfl
  map_zero' := rfl
  map_add' _ _ := rfl
#align algebraic_geometry.open_to_localization AlgebraicGeometry.openToLocalization

/-- The ring homomorphism from the stalk of the structure sheaf of `Proj` at a point corresponding
to a homogeneous prime ideal `x` to the *homogeneous localization* at `x`,
formed by gluing the `openToLocalization` maps. -/
def stalkToFiberRingHom (x : ProjectiveSpectrum.top ùíú) :
    (Proj.structureSheaf ùíú).presheaf.stalk x ‚ü∂ CommRingCat.of (at x) :=
  Limits.colimit.desc ((OpenNhds.inclusion x).op ‚ãô (Proj.structureSheaf ùíú).1)
    { pt := _
      Œπ :=
        { app := fun U =>
            openToLocalization ùíú ((OpenNhds.inclusion _).obj U.unop) x U.unop.2
          -- porting note: this proof was automatic in mathlib3
          naturality := fun _ _ _ => rfl } }
#align algebraic_geometry.stalk_to_fiber_ring_hom AlgebraicGeometry.stalkToFiberRingHom

@[simp]
theorem germ_comp_stalkToFiberRingHom (U : Opens (ProjectiveSpectrum.top ùíú)) (x : U) :
    (Proj.structureSheaf ùíú).presheaf.germ x ‚â´ stalkToFiberRingHom ùíú x =
      openToLocalization ùíú U x x.2 :=
  Limits.colimit.Œπ_desc _ _
#align algebraic_geometry.germ_comp_stalk_to_fiber_ring_hom AlgebraicGeometry.germ_comp_stalkToFiberRingHom

@[simp]
theorem stalkToFiberRingHom_germ' (U : Opens (ProjectiveSpectrum.top ùíú))
    (x : ProjectiveSpectrum.top ùíú) (hx : x ‚àà U) (s : (Proj.structureSheaf ùíú).1.obj (op U)) :
    stalkToFiberRingHom ùíú x ((Proj.structureSheaf ùíú).presheaf.germ ‚ü®x, hx‚ü© s) = (s.1 ‚ü®x, hx‚ü© : _) :=
  RingHom.ext_iff.1 (germ_comp_stalkToFiberRingHom ùíú U ‚ü®x, hx‚ü© : _) s
#align algebraic_geometry.stalk_to_fiber_ring_hom_germ' AlgebraicGeometry.stalkToFiberRingHom_germ'

@[simp]
theorem stalkToFiberRingHom_germ (U : Opens (ProjectiveSpectrum.top ùíú)) (x : U)
    (s : (Proj.structureSheaf ùíú).1.obj (op U)) :
    stalkToFiberRingHom ùíú x ((Proj.structureSheaf ùíú).presheaf.germ x s) = s.1 x :=
  stalkToFiberRingHom_germ' ùíú U _ _ _
#align algebraic_geometry.stalk_to_fiber_ring_hom_germ AlgebraicGeometry.stalkToFiberRingHom_germ

theorem HomogeneousLocalization.mem_basicOpen (x : ProjectiveSpectrum.top ùíú) (f : at x) :
    x ‚àà ProjectiveSpectrum.basicOpen ùíú f.den := by
  rw [ProjectiveSpectrum.mem_basicOpen]
  exact f.den_mem
#align algebraic_geometry.homogeneous_localization.mem_basic_open AlgebraicGeometry.HomogeneousLocalization.mem_basicOpen

/-- Given a point `x` corresponding to a homogeneous prime ideal, there is a (dependent) function
such that, for any `f` in the homogeneous localization at `x`, it returns the obvious section in the
basic open set `D(f.den)`-/
def sectionInBasicOpen (x : ProjectiveSpectrum.top ùíú) :
    ‚àÄ f : at x, (Proj.structureSheaf ùíú).1.obj (op (ProjectiveSpectrum.basicOpen ùíú f.den)) :=
  fun f =>
  ‚ü®fun y => Quotient.mk'' ‚ü®f.deg, ‚ü®f.num, f.num_mem_deg‚ü©, ‚ü®f.den, f.den_mem_deg‚ü©, y.2‚ü©, fun y =>
    ‚ü®ProjectiveSpectrum.basicOpen ùíú f.den, y.2,
      ‚ü®ùüô _, ‚ü®f.deg, ‚ü®‚ü®f.num, f.num_mem_deg‚ü©, ‚ü®f.den, f.den_mem_deg‚ü©, fun z => ‚ü®z.2, rfl‚ü©‚ü©‚ü©‚ü©‚ü©‚ü©
#align algebraic_geometry.section_in_basic_open AlgebraicGeometry.sectionInBasicOpen

/-- Given any point `x` and `f` in the homogeneous localization at `x`, there is an element in the
stalk at `x` obtained by `sectionInBasicOpen`. This is the inverse of `stalkToFiberRingHom`.
-/
def homogeneousLocalizationToStalk (x : ProjectiveSpectrum.top ùíú) :
    (at x) ‚Üí (Proj.structureSheaf ùíú).presheaf.stalk x := fun f =>
  (Proj.structureSheaf ùíú).presheaf.germ
    (‚ü®x, HomogeneousLocalization.mem_basicOpen _ x f‚ü© : ProjectiveSpectrum.basicOpen _ f.den)
    (sectionInBasicOpen _ x f)
#align algebraic_geometry.homogeneous_localization_to_stalk AlgebraicGeometry.homogeneousLocalizationToStalk

/-- Using `homogeneousLocalizationToStalk`, we construct a ring isomorphism between stalk at `x`
and homogeneous localization at `x` for any point `x` in `Proj`.-/
def Proj.stalkIso' (x : ProjectiveSpectrum.top ùíú) :
    (Proj.structureSheaf ùíú).presheaf.stalk x ‚âÉ+* CommRingCat.of (at x) :=
  RingEquiv.ofBijective (stalkToFiberRingHom _ x)
    ‚ü®fun z1 z2 eq1 => by
      obtain ‚ü®u1, memu1, s1, rfl‚ü© := (Proj.structureSheaf ùíú).presheaf.germ_exist x z1
      obtain ‚ü®u2, memu2, s2, rfl‚ü© := (Proj.structureSheaf ùíú).presheaf.germ_exist x z2
      obtain ‚ü®v1, memv1, i1, ‚ü®j1, ‚ü®a1, a1_mem‚ü©, ‚ü®b1, b1_mem‚ü©, hs1‚ü©‚ü© := s1.2 ‚ü®x, memu1‚ü©
      obtain ‚ü®v2, memv2, i2, ‚ü®j2, ‚ü®a2, a2_mem‚ü©, ‚ü®b2, b2_mem‚ü©, hs2‚ü©‚ü© := s2.2 ‚ü®x, memu2‚ü©
      obtain ‚ü®b1_nin_x, eq2‚ü© := hs1 ‚ü®x, memv1‚ü©
      obtain ‚ü®b2_nin_x, eq3‚ü© := hs2 ‚ü®x, memv2‚ü©
      dsimp only at eq1 eq2 eq3
      erw [stalkToFiberRingHom_germ ùíú u1 ‚ü®x, memu1‚ü© s1,
        stalkToFiberRingHom_germ ùíú u2 ‚ü®x, memu2‚ü© s2] at eq1
      erw [eq1] at eq2
      erw [eq2, Quotient.eq''] at eq3
      change Localization.mk _ _ = Localization.mk _ _ at eq3
      rw [Localization.mk_eq_mk', IsLocalization.eq] at eq3
      obtain ‚ü®‚ü®c, hc‚ü©, eq3‚ü© := eq3
      have eq3' :
        ‚àÄ (y : ProjectiveSpectrum.top ùíú)
          (hy : y ‚àà ProjectiveSpectrum.basicOpen ùíú b1 ‚äì ProjectiveSpectrum.basicOpen ùíú b2 ‚äì
                ProjectiveSpectrum.basicOpen ùíú c), (Localization.mk a1 ‚ü®b1,
                  show b1 ‚àâ y.asHomogeneousIdeal by
                    rw [‚Üê ProjectiveSpectrum.mem_basicOpen]
                    exact leOfHom (Opens.infLELeft _ _ ‚â´ Opens.infLELeft _ _) hy‚ü© :
              Localization.AtPrime y.1.toIdeal) = Localization.mk a2
              ‚ü®b2, by
                change b2 ‚àâ y.asHomogeneousIdeal
                rw [‚Üê ProjectiveSpectrum.mem_basicOpen]
                exact leOfHom (Opens.infLELeft _ _ ‚â´ Opens.infLERight _ _) hy‚ü© := by
        intro y hy
        rw [Localization.mk_eq_mk', IsLocalization.eq]
        exact
          ‚ü®‚ü®c, show c ‚àâ y.asHomogeneousIdeal by
                  rw [‚Üê ProjectiveSpectrum.mem_basicOpen]
                  exact leOfHom (Opens.infLERight _ _) hy‚ü©, eq3‚ü©
      refine'
        Presheaf.germ_ext (Proj.structureSheaf ùíú).1
          (ProjectiveSpectrum.basicOpen _ b1 ‚äì ProjectiveSpectrum.basicOpen _ b2 ‚äì
                ProjectiveSpectrum.basicOpen _ c ‚äì v1 ‚äì v2)
          ‚ü®‚ü®‚ü®‚ü®b1_nin_x, b2_nin_x‚ü©, hc‚ü©, memv1‚ü©, memv2‚ü©
          (Opens.infLELeft _ _ ‚â´ Opens.infLERight _ _ ‚â´ i1) (Opens.infLERight _ _ ‚â´ i2) _
      rw [Subtype.ext_iff_val]
      ext1 y
      simp only [res_apply]
      obtain ‚ü®b1_nin_y, eq6‚ü© :=
        hs1 ‚ü®_, leOfHom (Opens.infLELeft _ _ ‚â´ Opens.infLERight _ _) y.2‚ü©
      obtain ‚ü®b2_nin_y, eq7‚ü© := hs2 ‚ü®_, leOfHom (Opens.infLERight _ _) y.2‚ü©
      simp only at eq6 eq7
      erw [eq6, eq7, Quotient.eq'']
      change Localization.mk _ _ = Localization.mk _ _
      exact eq3' _
          ‚ü®‚ü®leOfHom
                (Opens.infLELeft _ _ ‚â´
                  Opens.infLELeft _ _ ‚â´ Opens.infLELeft _ _ ‚â´ Opens.infLELeft _ _) y.2,
              leOfHom
                (Opens.infLELeft _ _ ‚â´
                  Opens.infLELeft _ _ ‚â´ Opens.infLELeft _ _ ‚â´ Opens.infLERight _ _) y.2‚ü©,
            leOfHom (Opens.infLELeft _ _ ‚â´ Opens.infLELeft _ _ ‚â´ Opens.infLERight _ _) y.2‚ü©,
      Function.surjective_iff_hasRightInverse.mpr
        ‚ü®homogeneousLocalizationToStalk ùíú x, fun f => by
          rw [homogeneousLocalizationToStalk]
          erw [stalkToFiberRingHom_germ ùíú (ProjectiveSpectrum.basicOpen ùíú f.den) ‚ü®x, _‚ü©
              (sectionInBasicOpen _ x f)]
          rw [sectionInBasicOpen, HomogeneousLocalization.ext_iff_val, f.eq_num_div_den]
          simp only [unop_op, HomogeneousLocalization.val_mk'', mk_eq_monoidOf_mk'] ‚ü©‚ü©
set_option linter.uppercaseLean3 false in
#align algebraic_geometry.Proj.stalk_iso' AlgebraicGeometry.Proj.stalkIso'

/-- `Proj` of a graded ring as a `LocallyRingedSpace`-/
def Proj.toLocallyRingedSpace : LocallyRingedSpace :=
  { Proj.toSheafedSpace ùíú with
    localRing := fun x =>
      @RingEquiv.localRing _ _ _ (show LocalRing (at x) from inferInstance) _
        (Proj.stalkIso' ùíú x).symm }
set_option linter.uppercaseLean3 false in
#align algebraic_geometry.Proj.to_LocallyRingedSpace AlgebraicGeometry.Proj.toLocallyRingedSpace

end

end AlgebraicGeometry
