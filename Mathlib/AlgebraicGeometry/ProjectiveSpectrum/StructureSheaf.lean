/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import Mathlib.AlgebraicGeometry.ProjectiveSpectrum.Topology
import Mathlib.Topology.Sheaves.LocalPredicate
import Mathlib.RingTheory.GradedAlgebra.HomogeneousLocalization
import Mathlib.Geometry.RingedSpace.LocallyRingedSpace

#align_import algebraic_geometry.projective_spectrum.structure_sheaf from "leanprover-community/mathlib"@"486cb2f3bda4a67557c6285f5bd0c3348c1eea81"

/-!
# The structure sheaf on `projective_spectrum ğ’œ`.

In `Mathlib.AlgebraicGeometry.Topology`, we have given a topology on `ProjectiveSpectrum ğ’œ`; in
this file we will construct a sheaf on `ProjectiveSpectrum ğ’œ`.

## Notation
- `R` is a commutative semiring;
- `A` is a commutative ring and an `R`-algebra;
- `ğ’œ : â„• â†’ Submodule R A` is the grading of `A`;
- `U` is opposite object of some open subset of `ProjectiveSpectrum.top`.

## Main definitions and results
We define the structure sheaf as the subsheaf of all dependent function
`f : Î  x : U, HomogeneousLocalization ğ’œ x` such that `f` is locally expressible as ratio of two
elements of the *same grading*, i.e. `âˆ€ y âˆˆ U, âˆƒ (V âŠ† U) (i : â„•) (a b âˆˆ ğ’œ i), âˆ€ z âˆˆ V, f z = a / b`.

* `AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.isLocallyFraction`: the predicate that
  a dependent function is locally expressible as a ratio of two elements of the same grading.
* `AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.sectionsSubring`: the dependent functions
  satisfying the above local property forms a subring of all dependent functions
  `Î  x : U, HomogeneousLocalization ğ’œ x`.
* `AlgebraicGeometry.Proj.StructureSheaf`: the sheaf with `U â†¦ sectionsSubring U` and natural
  restriction map.

Then we establish that `Proj ğ’œ` is a `LocallyRingedSpace`:
* `AlgebraicGeometry.Proj.stalkIso'`: for any `x : ProjectiveSpectrum ğ’œ`, the stalk of
  `Proj.StructureSheaf` at `x` is isomorphic to `HomogeneousLocalization ğ’œ x`.
* `AlgebraicGeometry.Proj.toLocallyRingedSpace`: `Proj` as a locally ringed space.

## References

* [Robin Hartshorne, *Algebraic Geometry*][Har77]


-/


noncomputable section

namespace AlgebraicGeometry

open scoped DirectSum BigOperators Pointwise

open DirectSum SetLike Localization TopCat TopologicalSpace CategoryTheory Opposite

variable {R A : Type*}

variable [CommRing R] [CommRing A] [Algebra R A]

variable (ğ’œ : â„• â†’ Submodule R A) [GradedAlgebra ğ’œ]

local notation3 "at " x =>
  HomogeneousLocalization.AtPrime ğ’œ
    (HomogeneousIdeal.toIdeal (ProjectiveSpectrum.asHomogeneousIdeal x))

namespace ProjectiveSpectrum.StructureSheaf

variable {ğ’œ}

/-- The predicate saying that a dependent function on an open `U` is realised as a fixed fraction
`r / s` of *same grading* in each of the stalks (which are localizations at various prime ideals).
-/
def IsFraction {U : Opens (ProjectiveSpectrum.top ğ’œ)} (f : âˆ€ x : U, at x.1) : Prop :=
  âˆƒ (i : â„•) (r s : ğ’œ i),
    âˆ€ x : U, âˆƒ s_nin : s.1 âˆ‰ x.1.asHomogeneousIdeal, f x = Quotient.mk'' âŸ¨i, r, s, s_ninâŸ©
variable (ğ’œ)

/--
The predicate `IsFraction` is "prelocal", in the sense that if it holds on `U` it holds on any open
subset `V` of `U`.
-/
def isFractionPrelocal : PrelocalPredicate fun x : ProjectiveSpectrum.top ğ’œ => at x where
  pred f := IsFraction f
  res := by rintro V U i f âŸ¨j, r, s, wâŸ©; exact âŸ¨j, r, s, fun y => w (i y)âŸ©

/-- We will define the structure sheaf as the subsheaf of all dependent functions in
`Î  x : U, HomogeneousLocalization ğ’œ x` consisting of those functions which can locally be expressed
as a ratio of `A` of same grading.-/
def isLocallyFraction : LocalPredicate fun x : ProjectiveSpectrum.top ğ’œ => at x :=
  (isFractionPrelocal ğ’œ).sheafify

namespace SectionSubring

variable {ğ’œ}

open Submodule SetLike.GradedMonoid HomogeneousLocalization

theorem zeroMem' (U : (Opens (ProjectiveSpectrum.top ğ’œ))áµ’áµ–) :
    (isLocallyFraction ğ’œ).pred (0 : âˆ€ x : U.unop, at x.1) := fun x =>
  âŸ¨unop U, x.2, ğŸ™ (unop U), âŸ¨0, âŸ¨0, zero_mem _âŸ©, âŸ¨1, one_mem_graded _âŸ©, fun _ => âŸ¨_, rflâŸ©âŸ©âŸ©

theorem oneMem' (U : (Opens (ProjectiveSpectrum.top ğ’œ))áµ’áµ–) :
    (isLocallyFraction ğ’œ).pred (1 : âˆ€ x : U.unop, at x.1) := fun x =>
  âŸ¨unop U, x.2, ğŸ™ (unop U), âŸ¨0, âŸ¨1, one_mem_graded _âŸ©, âŸ¨1, one_mem_graded _âŸ©, fun _ => âŸ¨_, rflâŸ©âŸ©âŸ©

theorem addMem' (U : (Opens (ProjectiveSpectrum.top ğ’œ))áµ’áµ–) (a b : âˆ€ x : U.unop, at x.1)
    (ha : (isLocallyFraction ğ’œ).pred a) (hb : (isLocallyFraction ğ’œ).pred b) :
    (isLocallyFraction ğ’œ).pred (a + b) := fun x => by
  rcases ha x with âŸ¨Va, ma, ia, ja, âŸ¨ra, ra_memâŸ©, âŸ¨sa, sa_memâŸ©, waâŸ©
  rcases hb x with âŸ¨Vb, mb, ib, jb, âŸ¨rb, rb_memâŸ©, âŸ¨sb, sb_memâŸ©, wbâŸ©
  refine'
    âŸ¨Va âŠ“ Vb, âŸ¨ma, mbâŸ©, Opens.infLELeft _ _ â‰« ia, ja + jb,
      âŸ¨sb * ra + sa * rb,
        add_mem (add_comm jb ja â–¸ mul_mem_graded sb_mem ra_mem : sb * ra âˆˆ ğ’œ (ja + jb))
          (mul_mem_graded sa_mem rb_mem)âŸ©,
      âŸ¨sa * sb, mul_mem_graded sa_mem sb_memâŸ©, fun y => âŸ¨fun h => _, _âŸ©âŸ©
  Â· cases' (y : ProjectiveSpectrum.top ğ’œ).isPrime.mem_or_mem h with h h
    Â· obtain âŸ¨nin, -âŸ© := wa âŸ¨y, (Opens.infLELeft Va Vb y).2âŸ©; exact nin h
    Â· obtain âŸ¨nin, -âŸ© := wb âŸ¨y, (Opens.infLERight Va Vb y).2âŸ©; exact nin h
  Â· simp only [add_mul, map_add, Pi.add_apply, RingHom.map_mul, ext_iff_val, add_val]
    obtain âŸ¨nin1, hy1âŸ© := wa (Opens.infLELeft Va Vb y)
    obtain âŸ¨nin2, hy2âŸ© := wb (Opens.infLERight Va Vb y)
    dsimp only at hy1 hy2
    erw [hy1, hy2]
    simp only [val_mk'', add_mk, add_comm (sa * rb)]
    rfl

theorem negMem' (U : (Opens (ProjectiveSpectrum.top ğ’œ))áµ’áµ–) (a : âˆ€ x : U.unop, at x.1)
    (ha : (isLocallyFraction ğ’œ).pred a) : (isLocallyFraction ğ’œ).pred (-a) := fun x => by
  rcases ha x with âŸ¨V, m, i, j, âŸ¨r, r_memâŸ©, âŸ¨s, s_memâŸ©, wâŸ©
  choose nin hy using w
  refine' âŸ¨V, m, i, j, âŸ¨-r, Submodule.neg_mem _ r_memâŸ©, âŸ¨s, s_memâŸ©, fun y => âŸ¨nin y, _âŸ©âŸ©
  simp only [ext_iff_val, val_mk''] at hy
  simp only [Pi.neg_apply, ext_iff_val, neg_val, hy, val_mk'', neg_mk]

theorem mulMem' (U : (Opens (ProjectiveSpectrum.top ğ’œ))áµ’áµ–) (a b : âˆ€ x : U.unop, at x.1)
    (ha : (isLocallyFraction ğ’œ).pred a) (hb : (isLocallyFraction ğ’œ).pred b) :
    (isLocallyFraction ğ’œ).pred (a * b) := fun x => by
  rcases ha x with âŸ¨Va, ma, ia, ja, âŸ¨ra, ra_memâŸ©, âŸ¨sa, sa_memâŸ©, waâŸ©
  rcases hb x with âŸ¨Vb, mb, ib, jb, âŸ¨rb, rb_memâŸ©, âŸ¨sb, sb_memâŸ©, wbâŸ©
  refine'
    âŸ¨Va âŠ“ Vb, âŸ¨ma, mbâŸ©, Opens.infLELeft _ _ â‰« ia, ja + jb,
      âŸ¨ra * rb, SetLike.mul_mem_graded ra_mem rb_memâŸ©,
      âŸ¨sa * sb, SetLike.mul_mem_graded sa_mem sb_memâŸ©, fun y => âŸ¨fun h => _, _âŸ©âŸ©
  Â· cases' (y : ProjectiveSpectrum.top ğ’œ).isPrime.mem_or_mem h with h h
    Â· choose nin _ using wa âŸ¨y, (Opens.infLELeft Va Vb y).2âŸ©; exact nin h
    Â· choose nin _ using wb âŸ¨y, (Opens.infLERight Va Vb y).2âŸ©; exact nin h
  Â· simp only [Pi.mul_apply, RingHom.map_mul]
    choose nin1 hy1 using wa (Opens.infLELeft Va Vb y)
    choose nin2 hy2 using wb (Opens.infLERight Va Vb y)
    rw [ext_iff_val] at hy1 hy2 âŠ¢
    erw [mul_val, hy1, hy2]
    simp only [val_mk'', mk_mul]
    rfl

end SectionSubring

section

open SectionSubring

variable {ğ’œ}

/-- The functions satisfying `isLocallyFraction` form a subring of all dependent functions
`Î  x : U, HomogeneousLocalization ğ’œ x`.-/
def sectionsSubring (U : (Opens (ProjectiveSpectrum.top ğ’œ))áµ’áµ–) : Subring (âˆ€ x : U.unop, at x.1)
    where
  carrier := {f | (isLocallyFraction ğ’œ).pred f}
  zero_mem' := zeroMem' U
  one_mem' := oneMem' U
  add_mem' := addMem' U _ _
  neg_mem' := negMem' U _
  mul_mem' := mulMem' U _ _

end

/-- The structure sheaf (valued in `Type`, not yet `CommRing`) is the subsheaf consisting of
functions satisfying `isLocallyFraction`.-/
def structureSheafInType : Sheaf (Type _) (ProjectiveSpectrum.top ğ’œ) :=
  subsheafToTypes (isLocallyFraction ğ’œ)

instance commRingStructureSheafInTypeObj (U : (Opens (ProjectiveSpectrum.top ğ’œ))áµ’áµ–) :
    CommRing ((structureSheafInType ğ’œ).1.obj U) :=
  (sectionsSubring U).toCommRing

/-- The structure presheaf, valued in `CommRing`, constructed by dressing up the `Type` valued
structure presheaf.-/
@[simps]
def structurePresheafInCommRing : Presheaf CommRingCat (ProjectiveSpectrum.top ğ’œ) where
  obj U := CommRingCat.of ((structureSheafInType ğ’œ).1.obj U)
  map i :=
    { toFun := (structureSheafInType ğ’œ).1.map i
      map_zero' := rfl
      map_add' := fun x y => rfl
      map_one' := rfl
      map_mul' := fun x y => rfl }

-- These lemmas have always been bad (#7657), but leanprover/lean4#2644 made `simp` start noticing
attribute [nolint simpNF]
  AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.structurePresheafInCommRing_map_apply

/-- Some glue, verifying that that structure presheaf valued in `CommRing` agrees with the `Type`
valued structure presheaf.-/
def structurePresheafCompForget :
    structurePresheafInCommRing ğ’œ â‹™ forget CommRingCat â‰… (structureSheafInType ğ’œ).1 :=
  NatIso.ofComponents (fun U => Iso.refl _) (by aesop_cat)

end ProjectiveSpectrum.StructureSheaf

namespace ProjectiveSpectrum

open TopCat.Presheaf ProjectiveSpectrum.StructureSheaf Opens

/-- The structure sheaf on `Proj` ğ’œ, valued in `CommRing`.-/
def Proj.structureSheaf : Sheaf CommRingCat (ProjectiveSpectrum.top ğ’œ) :=
  âŸ¨structurePresheafInCommRing ğ’œ,
    (-- We check the sheaf condition under `forget CommRing`.
          isSheaf_iff_isSheaf_comp
          _ _).mpr
      (isSheaf_of_iso (structurePresheafCompForget ğ’œ).symm (structureSheafInType ğ’œ).cond)âŸ©

end ProjectiveSpectrum

section

open ProjectiveSpectrum ProjectiveSpectrum.StructureSheaf Opens

@[simp]
theorem res_apply (U V : Opens (ProjectiveSpectrum.top ğ’œ)) (i : V âŸ¶ U)
    (s : (Proj.structureSheaf ğ’œ).1.obj (op U)) (x : V) :
    ((Proj.structureSheaf ğ’œ).1.map i.op s).1 x = (s.1 (i x) : _) :=
  rfl

/-- `Proj` of a graded ring as a `SheafedSpace`-/
def Proj.toSheafedSpace : SheafedSpace CommRingCat where
  carrier := TopCat.of (ProjectiveSpectrum ğ’œ)
  presheaf := (Proj.structureSheaf ğ’œ).1
  IsSheaf := (Proj.structureSheaf ğ’œ).2

/-- The ring homomorphism that takes a section of the structure sheaf of `Proj` on the open set `U`,
implemented as a subtype of dependent functions to localizations at homogeneous prime ideals, and
evaluates the section on the point corresponding to a given homogeneous prime ideal. -/
def openToLocalization (U : Opens (ProjectiveSpectrum.top ğ’œ)) (x : ProjectiveSpectrum.top ğ’œ)
    (hx : x âˆˆ U) : (Proj.structureSheaf ğ’œ).1.obj (op U) âŸ¶ CommRingCat.of (at x) where
  toFun s := (s.1 âŸ¨x, hxâŸ© : _)
  map_one' := rfl
  map_mul' _ _ := rfl
  map_zero' := rfl
  map_add' _ _ := rfl

/-- The ring homomorphism from the stalk of the structure sheaf of `Proj` at a point corresponding
to a homogeneous prime ideal `x` to the *homogeneous localization* at `x`,
formed by gluing the `openToLocalization` maps. -/
def stalkToFiberRingHom (x : ProjectiveSpectrum.top ğ’œ) :
    (Proj.structureSheaf ğ’œ).presheaf.stalk x âŸ¶ CommRingCat.of (at x) :=
  Limits.colimit.desc ((OpenNhds.inclusion x).op â‹™ (Proj.structureSheaf ğ’œ).1)
    { pt := _
      Î¹ :=
        { app := fun U =>
            openToLocalization ğ’œ ((OpenNhds.inclusion _).obj U.unop) x U.unop.2
          -- porting note: this proof was automatic in mathlib3
          naturality := fun _ _ _ => rfl } }

@[simp]
theorem germ_comp_stalkToFiberRingHom (U : Opens (ProjectiveSpectrum.top ğ’œ)) (x : U) :
    (Proj.structureSheaf ğ’œ).presheaf.germ x â‰« stalkToFiberRingHom ğ’œ x =
      openToLocalization ğ’œ U x x.2 :=
  Limits.colimit.Î¹_desc _ _

@[simp]
theorem stalkToFiberRingHom_germ' (U : Opens (ProjectiveSpectrum.top ğ’œ))
    (x : ProjectiveSpectrum.top ğ’œ) (hx : x âˆˆ U) (s : (Proj.structureSheaf ğ’œ).1.obj (op U)) :
    stalkToFiberRingHom ğ’œ x ((Proj.structureSheaf ğ’œ).presheaf.germ âŸ¨x, hxâŸ© s) = (s.1 âŸ¨x, hxâŸ© : _) :=
  RingHom.ext_iff.1 (germ_comp_stalkToFiberRingHom ğ’œ U âŸ¨x, hxâŸ© : _) s

@[simp]
theorem stalkToFiberRingHom_germ (U : Opens (ProjectiveSpectrum.top ğ’œ)) (x : U)
    (s : (Proj.structureSheaf ğ’œ).1.obj (op U)) :
    stalkToFiberRingHom ğ’œ x ((Proj.structureSheaf ğ’œ).presheaf.germ x s) = s.1 x :=
  stalkToFiberRingHom_germ' ğ’œ U _ _ _

theorem HomogeneousLocalization.mem_basicOpen (x : ProjectiveSpectrum.top ğ’œ) (f : at x) :
    x âˆˆ ProjectiveSpectrum.basicOpen ğ’œ f.den := by
  rw [ProjectiveSpectrum.mem_basicOpen]
  exact f.den_mem

/-- Given a point `x` corresponding to a homogeneous prime ideal, there is a (dependent) function
such that, for any `f` in the homogeneous localization at `x`, it returns the obvious section in the
basic open set `D(f.den)`-/
def sectionInBasicOpen (x : ProjectiveSpectrum.top ğ’œ) :
    âˆ€ f : at x, (Proj.structureSheaf ğ’œ).1.obj (op (ProjectiveSpectrum.basicOpen ğ’œ f.den)) :=
  fun f =>
  âŸ¨fun y => Quotient.mk'' âŸ¨f.deg, âŸ¨f.num, f.num_mem_degâŸ©, âŸ¨f.den, f.den_mem_degâŸ©, y.2âŸ©, fun y =>
    âŸ¨ProjectiveSpectrum.basicOpen ğ’œ f.den, y.2,
      âŸ¨ğŸ™ _, âŸ¨f.deg, âŸ¨âŸ¨f.num, f.num_mem_degâŸ©, âŸ¨f.den, f.den_mem_degâŸ©, fun z => âŸ¨z.2, rflâŸ©âŸ©âŸ©âŸ©âŸ©âŸ©

/-- Given any point `x` and `f` in the homogeneous localization at `x`, there is an element in the
stalk at `x` obtained by `sectionInBasicOpen`. This is the inverse of `stalkToFiberRingHom`.
-/
def homogeneousLocalizationToStalk (x : ProjectiveSpectrum.top ğ’œ) :
    (at x) â†’ (Proj.structureSheaf ğ’œ).presheaf.stalk x := fun f =>
  (Proj.structureSheaf ğ’œ).presheaf.germ
    (âŸ¨x, HomogeneousLocalization.mem_basicOpen _ x fâŸ© : ProjectiveSpectrum.basicOpen _ f.den)
    (sectionInBasicOpen _ x f)

/-- Using `homogeneousLocalizationToStalk`, we construct a ring isomorphism between stalk at `x`
and homogeneous localization at `x` for any point `x` in `Proj`.-/
def Proj.stalkIso' (x : ProjectiveSpectrum.top ğ’œ) :
    (Proj.structureSheaf ğ’œ).presheaf.stalk x â‰ƒ+* CommRingCat.of (at x) :=
  RingEquiv.ofBijective (stalkToFiberRingHom _ x)
    âŸ¨fun z1 z2 eq1 => by
      obtain âŸ¨u1, memu1, s1, rflâŸ© := (Proj.structureSheaf ğ’œ).presheaf.germ_exist x z1
      obtain âŸ¨u2, memu2, s2, rflâŸ© := (Proj.structureSheaf ğ’œ).presheaf.germ_exist x z2
      obtain âŸ¨v1, memv1, i1, âŸ¨j1, âŸ¨a1, a1_memâŸ©, âŸ¨b1, b1_memâŸ©, hs1âŸ©âŸ© := s1.2 âŸ¨x, memu1âŸ©
      obtain âŸ¨v2, memv2, i2, âŸ¨j2, âŸ¨a2, a2_memâŸ©, âŸ¨b2, b2_memâŸ©, hs2âŸ©âŸ© := s2.2 âŸ¨x, memu2âŸ©
      obtain âŸ¨b1_nin_x, eq2âŸ© := hs1 âŸ¨x, memv1âŸ©
      obtain âŸ¨b2_nin_x, eq3âŸ© := hs2 âŸ¨x, memv2âŸ©
      dsimp only at eq1 eq2 eq3
      erw [stalkToFiberRingHom_germ ğ’œ u1 âŸ¨x, memu1âŸ© s1,
        stalkToFiberRingHom_germ ğ’œ u2 âŸ¨x, memu2âŸ© s2] at eq1
      erw [eq1] at eq2
      erw [eq2, Quotient.eq''] at eq3
      change Localization.mk _ _ = Localization.mk _ _ at eq3
      rw [Localization.mk_eq_mk', IsLocalization.eq] at eq3
      obtain âŸ¨âŸ¨c, hcâŸ©, eq3âŸ© := eq3
      have eq3' :
        âˆ€ (y : ProjectiveSpectrum.top ğ’œ)
          (hy : y âˆˆ ProjectiveSpectrum.basicOpen ğ’œ b1 âŠ“ ProjectiveSpectrum.basicOpen ğ’œ b2 âŠ“
                ProjectiveSpectrum.basicOpen ğ’œ c), (Localization.mk a1 âŸ¨b1,
                  show b1 âˆ‰ y.asHomogeneousIdeal by
                    rw [â† ProjectiveSpectrum.mem_basicOpen]
                    exact leOfHom (Opens.infLELeft _ _ â‰« Opens.infLELeft _ _) hyâŸ© :
              Localization.AtPrime y.1.toIdeal) = Localization.mk a2
              âŸ¨b2, by
                change b2 âˆ‰ y.asHomogeneousIdeal
                rw [â† ProjectiveSpectrum.mem_basicOpen]
                exact leOfHom (Opens.infLELeft _ _ â‰« Opens.infLERight _ _) hyâŸ© := by
        intro y hy
        rw [Localization.mk_eq_mk', IsLocalization.eq]
        exact
          âŸ¨âŸ¨c, show c âˆ‰ y.asHomogeneousIdeal by
                  rw [â† ProjectiveSpectrum.mem_basicOpen]
                  exact leOfHom (Opens.infLERight _ _) hyâŸ©, eq3âŸ©
      refine'
        Presheaf.germ_ext (Proj.structureSheaf ğ’œ).1
          (ProjectiveSpectrum.basicOpen _ b1 âŠ“ ProjectiveSpectrum.basicOpen _ b2 âŠ“
                ProjectiveSpectrum.basicOpen _ c âŠ“ v1 âŠ“ v2)
          âŸ¨âŸ¨âŸ¨âŸ¨b1_nin_x, b2_nin_xâŸ©, hcâŸ©, memv1âŸ©, memv2âŸ©
          (Opens.infLELeft _ _ â‰« Opens.infLERight _ _ â‰« i1) (Opens.infLERight _ _ â‰« i2) _
      rw [Subtype.ext_iff_val]
      ext1 y
      simp only [res_apply]
      obtain âŸ¨b1_nin_y, eq6âŸ© :=
        hs1 âŸ¨_, leOfHom (Opens.infLELeft _ _ â‰« Opens.infLERight _ _) y.2âŸ©
      obtain âŸ¨b2_nin_y, eq7âŸ© := hs2 âŸ¨_, leOfHom (Opens.infLERight _ _) y.2âŸ©
      simp only at eq6 eq7
      erw [eq6, eq7, Quotient.eq'']
      change Localization.mk _ _ = Localization.mk _ _
      exact eq3' _
          âŸ¨âŸ¨leOfHom
                (Opens.infLELeft _ _ â‰«
                  Opens.infLELeft _ _ â‰« Opens.infLELeft _ _ â‰« Opens.infLELeft _ _) y.2,
              leOfHom
                (Opens.infLELeft _ _ â‰«
                  Opens.infLELeft _ _ â‰« Opens.infLELeft _ _ â‰« Opens.infLERight _ _) y.2âŸ©,
            leOfHom (Opens.infLELeft _ _ â‰« Opens.infLELeft _ _ â‰« Opens.infLERight _ _) y.2âŸ©,
      Function.surjective_iff_hasRightInverse.mpr
        âŸ¨homogeneousLocalizationToStalk ğ’œ x, fun f => by
          rw [homogeneousLocalizationToStalk]
          erw [stalkToFiberRingHom_germ ğ’œ (ProjectiveSpectrum.basicOpen ğ’œ f.den) âŸ¨x, _âŸ©
              (sectionInBasicOpen _ x f)]
          rw [sectionInBasicOpen, HomogeneousLocalization.ext_iff_val, f.eq_num_div_den]
          simp only [unop_op, HomogeneousLocalization.val_mk'', mk_eq_monoidOf_mk'] âŸ©âŸ©

/-- `Proj` of a graded ring as a `LocallyRingedSpace`-/
def Proj.toLocallyRingedSpace : LocallyRingedSpace :=
  { Proj.toSheafedSpace ğ’œ with
    localRing := fun x =>
      @RingEquiv.localRing _ _ _ (show LocalRing (at x) from inferInstance) _
        (Proj.stalkIso' ğ’œ x).symm }

end

end AlgebraicGeometry
