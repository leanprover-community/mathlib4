/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import Mathlib.AlgebraicGeometry.ProjectiveSpectrum.Topology
import Mathlib.Topology.Sheaves.LocalPredicate
import Mathlib.RingTheory.GradedAlgebra.HomogeneousLocalization
import Mathlib.Geometry.RingedSpace.LocallyRingedSpace

#align_import algebraic_geometry.projective_spectrum.structure_sheaf from "leanprover-community/mathlib"@"486cb2f3bda4a67557c6285f5bd0c3348c1eea81"

/-!
# The structure sheaf on `projective_spectrum ğ’œ`.

In `Mathlib.AlgebraicGeometry.Topology`, we have given a topology on `ProjectiveSpectrum ğ’œ`; in
this file we will construct a sheaf on `ProjectiveSpectrum ğ’œ`.

## Notation
- `R` is a commutative semiring;
- `A` is a commutative ring and an `R`-algebra;
- `ğ’œ : â„• â†’ Submodule R A` is the grading of `A`;
- `U` is opposite object of some open subset of `ProjectiveSpectrum.top`.

## Main definitions and results
We define the structure sheaf as the subsheaf of all dependent function
`f : Î  x : U, HomogeneousLocalization ğ’œ x` such that `f` is locally expressible as ratio of two
elements of the *same grading*, i.e. `âˆ€ y âˆˆ U, âˆƒ (V âŠ† U) (i : â„•) (a b âˆˆ ğ’œ i), âˆ€ z âˆˆ V, f z = a / b`.

* `AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.isLocallyFraction`: the predicate that
  a dependent function is locally expressible as a ratio of two elements of the same grading.
* `AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.sectionsSubring`: the dependent functions
  satisfying the above local property forms a subring of all dependent functions
  `Î  x : U, HomogeneousLocalization ğ’œ x`.
* `AlgebraicGeometry.Proj.StructureSheaf`: the sheaf with `U â†¦ sectionsSubring U` and natural
  restriction map.

Then we establish that `Proj ğ’œ` is a `LocallyRingedSpace`:
* `AlgebraicGeometry.Proj.stalkIso'`: for any `x : ProjectiveSpectrum ğ’œ`, the stalk of
  `Proj.StructureSheaf` at `x` is isomorphic to `HomogeneousLocalization ğ’œ x`.
* `AlgebraicGeometry.Proj.toLocallyRingedSpace`: `Proj` as a locally ringed space.

## References

* [Robin Hartshorne, *Algebraic Geometry*][Har77]


-/


noncomputable section

namespace AlgebraicGeometry

open scoped DirectSum Pointwise

open DirectSum SetLike Localization TopCat TopologicalSpace CategoryTheory Opposite

variable {R A : Type*}
variable [CommRing R] [CommRing A] [Algebra R A]
variable (ğ’œ : â„• â†’ Submodule R A) [GradedAlgebra ğ’œ]

local notation3 "at " x =>
  HomogeneousLocalization.AtPrime ğ’œ
    (HomogeneousIdeal.toIdeal (ProjectiveSpectrum.asHomogeneousIdeal x))

namespace ProjectiveSpectrum.StructureSheaf

variable {ğ’œ}

/-- The predicate saying that a dependent function on an open `U` is realised as a fixed fraction
`r / s` of *same grading* in each of the stalks (which are localizations at various prime ideals).
-/
def IsFraction {U : Opens (ProjectiveSpectrum.top ğ’œ)} (f : âˆ€ x : U, at x.1) : Prop :=
  âˆƒ (i : â„•) (r s : ğ’œ i) (s_nin : âˆ€ x : U, s.1 âˆ‰ x.1.asHomogeneousIdeal),
    âˆ€ x : U, f x = .mk âŸ¨i, r, s, s_nin xâŸ©
#align algebraic_geometry.projective_spectrum.structure_sheaf.is_fraction AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.IsFraction
variable (ğ’œ)

/--
The predicate `IsFraction` is "prelocal", in the sense that if it holds on `U` it holds on any open
subset `V` of `U`.
-/
def isFractionPrelocal : PrelocalPredicate fun x : ProjectiveSpectrum.top ğ’œ => at x where
  pred f := IsFraction f
  res := by rintro V U i f âŸ¨j, r, s, h, wâŸ©; exact âŸ¨j, r, s, (h <| i Â·), (w <| i Â·)âŸ©
#align algebraic_geometry.projective_spectrum.structure_sheaf.is_fraction_prelocal AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.isFractionPrelocal

/-- We will define the structure sheaf as the subsheaf of all dependent functions in
`Î  x : U, HomogeneousLocalization ğ’œ x` consisting of those functions which can locally be expressed
as a ratio of `A` of same grading. -/
def isLocallyFraction : LocalPredicate fun x : ProjectiveSpectrum.top ğ’œ => at x :=
  (isFractionPrelocal ğ’œ).sheafify
#align algebraic_geometry.projective_spectrum.structure_sheaf.is_locally_fraction AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.isLocallyFraction

namespace SectionSubring

variable {ğ’œ}

open Submodule SetLike.GradedMonoid HomogeneousLocalization

theorem zero_mem' (U : (Opens (ProjectiveSpectrum.top ğ’œ))áµ’áµ–) :
    (isLocallyFraction ğ’œ).pred (0 : âˆ€ x : U.unop, at x.1) := fun x =>
  âŸ¨unop U, x.2, ğŸ™ (unop U), âŸ¨0, âŸ¨0, zero_mem _âŸ©, âŸ¨1, one_mem_graded _âŸ©, _, fun _ => rflâŸ©âŸ©
#align algebraic_geometry.projective_spectrum.structure_sheaf.section_subring.zero_mem' AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.SectionSubring.zero_mem'

theorem one_mem' (U : (Opens (ProjectiveSpectrum.top ğ’œ))áµ’áµ–) :
    (isLocallyFraction ğ’œ).pred (1 : âˆ€ x : U.unop, at x.1) := fun x =>
  âŸ¨unop U, x.2, ğŸ™ (unop U), âŸ¨0, âŸ¨1, one_mem_graded _âŸ©, âŸ¨1, one_mem_graded _âŸ©, _, fun _ => rflâŸ©âŸ©
#align algebraic_geometry.projective_spectrum.structure_sheaf.section_subring.one_mem' AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.SectionSubring.one_mem'

theorem add_mem' (U : (Opens (ProjectiveSpectrum.top ğ’œ))áµ’áµ–) (a b : âˆ€ x : U.unop, at x.1)
    (ha : (isLocallyFraction ğ’œ).pred a) (hb : (isLocallyFraction ğ’œ).pred b) :
    (isLocallyFraction ğ’œ).pred (a + b) := fun x => by
  rcases ha x with âŸ¨Va, ma, ia, ja, âŸ¨ra, ra_memâŸ©, âŸ¨sa, sa_memâŸ©, hwa, waâŸ©
  rcases hb x with âŸ¨Vb, mb, ib, jb, âŸ¨rb, rb_memâŸ©, âŸ¨sb, sb_memâŸ©, hwb, wbâŸ©
  refine
    âŸ¨Va âŠ“ Vb, âŸ¨ma, mbâŸ©, Opens.infLELeft _ _ â‰« ia, ja + jb,
      âŸ¨sb * ra + sa * rb,
        add_mem (add_comm jb ja â–¸ mul_mem_graded sb_mem ra_mem : sb * ra âˆˆ ğ’œ (ja + jb))
          (mul_mem_graded sa_mem rb_mem)âŸ©,
      âŸ¨sa * sb, mul_mem_graded sa_mem sb_memâŸ©, fun y â†¦
        y.1.asHomogeneousIdeal.toIdeal.primeCompl.mul_mem (hwa âŸ¨y.1, y.2.1âŸ©) (hwb âŸ¨y.1, y.2.2âŸ©),
      fun y => ?_âŸ©
  simp only at wa wb
  simp only [Pi.add_apply, wa âŸ¨y.1, y.2.1âŸ©, wb âŸ¨y.1, y.2.2âŸ©, ext_iff_val,
    val_add, val_mk, add_mk, add_comm (sa * rb)]
  rfl
#align algebraic_geometry.projective_spectrum.structure_sheaf.section_subring.add_mem' AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.SectionSubring.add_mem'

theorem neg_mem' (U : (Opens (ProjectiveSpectrum.top ğ’œ))áµ’áµ–) (a : âˆ€ x : U.unop, at x.1)
    (ha : (isLocallyFraction ğ’œ).pred a) : (isLocallyFraction ğ’œ).pred (-a) := fun x => by
  rcases ha x with âŸ¨V, m, i, j, âŸ¨r, r_memâŸ©, âŸ¨s, s_memâŸ©, nin, hyâŸ©
  refine âŸ¨V, m, i, j, âŸ¨-r, Submodule.neg_mem _ r_memâŸ©, âŸ¨s, s_memâŸ©, nin, fun y => ?_âŸ©
  simp only [ext_iff_val, val_mk] at hy
  simp only [Pi.neg_apply, ext_iff_val, val_neg, hy, val_mk, neg_mk]
#align algebraic_geometry.projective_spectrum.structure_sheaf.section_subring.neg_mem' AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.SectionSubring.neg_mem'

theorem mul_mem' (U : (Opens (ProjectiveSpectrum.top ğ’œ))áµ’áµ–) (a b : âˆ€ x : U.unop, at x.1)
    (ha : (isLocallyFraction ğ’œ).pred a) (hb : (isLocallyFraction ğ’œ).pred b) :
    (isLocallyFraction ğ’œ).pred (a * b) := fun x => by
  rcases ha x with âŸ¨Va, ma, ia, ja, âŸ¨ra, ra_memâŸ©, âŸ¨sa, sa_memâŸ©, hwa, waâŸ©
  rcases hb x with âŸ¨Vb, mb, ib, jb, âŸ¨rb, rb_memâŸ©, âŸ¨sb, sb_memâŸ©, hwb, wbâŸ©
  refine
    âŸ¨Va âŠ“ Vb, âŸ¨ma, mbâŸ©, Opens.infLELeft _ _ â‰« ia, ja + jb,
      âŸ¨ra * rb, SetLike.mul_mem_graded ra_mem rb_memâŸ©,
      âŸ¨sa * sb, SetLike.mul_mem_graded sa_mem sb_memâŸ©, fun y =>
      y.1.asHomogeneousIdeal.toIdeal.primeCompl.mul_mem (hwa âŸ¨y.1, y.2.1âŸ©) (hwb âŸ¨y.1, y.2.2âŸ©),
      fun y â†¦ ?_âŸ©
  simp only [Pi.mul_apply, wa âŸ¨y.1, y.2.1âŸ©, wb âŸ¨y.1, y.2.2âŸ©, ext_iff_val, val_mul, val_mk,
    Localization.mk_mul]
  rfl
#align algebraic_geometry.projective_spectrum.structure_sheaf.section_subring.mul_mem' AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.SectionSubring.mul_mem'

end SectionSubring

section

open SectionSubring

variable {ğ’œ}

/-- The functions satisfying `isLocallyFraction` form a subring of all dependent functions
`Î  x : U, HomogeneousLocalization ğ’œ x`. -/
def sectionsSubring (U : (Opens (ProjectiveSpectrum.top ğ’œ))áµ’áµ–) :
    Subring (âˆ€ x : U.unop, at x.1) where
  carrier := {f | (isLocallyFraction ğ’œ).pred f}
  zero_mem' := zero_mem' U
  one_mem' := one_mem' U
  add_mem' := add_mem' U _ _
  neg_mem' := neg_mem' U _
  mul_mem' := mul_mem' U _ _
#align algebraic_geometry.projective_spectrum.structure_sheaf.sections_subring AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.sectionsSubring

end

/-- The structure sheaf (valued in `Type`, not yet `CommRing`) is the subsheaf consisting of
functions satisfying `isLocallyFraction`. -/
def structureSheafInType : Sheaf (Type _) (ProjectiveSpectrum.top ğ’œ) :=
  subsheafToTypes (isLocallyFraction ğ’œ)
#align algebraic_geometry.projective_spectrum.structure_sheaf.structure_sheaf_in_Type AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.structureSheafInType

instance commRingStructureSheafInTypeObj (U : (Opens (ProjectiveSpectrum.top ğ’œ))áµ’áµ–) :
    CommRing ((structureSheafInType ğ’œ).1.obj U) :=
  (sectionsSubring U).toCommRing
#align algebraic_geometry.projective_spectrum.structure_sheaf.comm_ring_structure_sheaf_in_Type_obj AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.commRingStructureSheafInTypeObj

/-- The structure presheaf, valued in `CommRing`, constructed by dressing up the `Type` valued
structure presheaf. -/
@[simps]
def structurePresheafInCommRing : Presheaf CommRingCat (ProjectiveSpectrum.top ğ’œ) where
  obj U := CommRingCat.of ((structureSheafInType ğ’œ).1.obj U)
  map i :=
    { toFun := (structureSheafInType ğ’œ).1.map i
      map_zero' := rfl
      map_add' := fun x y => rfl
      map_one' := rfl
      map_mul' := fun x y => rfl }
set_option linter.uppercaseLean3 false in
#align algebraic_geometry.projective_spectrum.structure_sheaf.structure_presheaf_in_CommRing AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.structurePresheafInCommRing

-- These lemmas have always been bad (#7657), but leanprover/lean4#2644 made `simp` start noticing
attribute [nolint simpNF]
  AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.structurePresheafInCommRing_map_apply

/-- Some glue, verifying that the structure presheaf valued in `CommRing` agrees with the `Type`
valued structure presheaf. -/
def structurePresheafCompForget :
    structurePresheafInCommRing ğ’œ â‹™ forget CommRingCat â‰… (structureSheafInType ğ’œ).1 :=
  NatIso.ofComponents (fun U => Iso.refl _) (by aesop_cat)
#align algebraic_geometry.projective_spectrum.structure_sheaf.structure_presheaf_comp_forget AlgebraicGeometry.ProjectiveSpectrum.StructureSheaf.structurePresheafCompForget

end ProjectiveSpectrum.StructureSheaf

namespace ProjectiveSpectrum

open TopCat.Presheaf ProjectiveSpectrum.StructureSheaf Opens

/-- The structure sheaf on `Proj` ğ’œ, valued in `CommRing`. -/
def Proj.structureSheaf : Sheaf CommRingCat (ProjectiveSpectrum.top ğ’œ) :=
  âŸ¨structurePresheafInCommRing ğ’œ,
    (-- We check the sheaf condition under `forget CommRing`.
          isSheaf_iff_isSheaf_comp
          _ _).mpr
      (isSheaf_of_iso (structurePresheafCompForget ğ’œ).symm (structureSheafInType ğ’œ).cond)âŸ©
set_option linter.uppercaseLean3 false in
#align algebraic_geometry.projective_spectrum.Proj.structure_sheaf AlgebraicGeometry.ProjectiveSpectrum.Proj.structureSheaf

end ProjectiveSpectrum

section

open ProjectiveSpectrum ProjectiveSpectrum.StructureSheaf Opens

section
variable {U V : (Opens (ProjectiveSpectrum.top ğ’œ))áµ’áµ–} (i : V âŸ¶ U)
    (s t : (Proj.structureSheaf ğ’œ).1.obj V) (x : V.unop)

@[simp]
theorem Proj.res_apply (x) : ((Proj.structureSheaf ğ’œ).1.map i s).1 x = s.1 (i.unop x) := rfl
#align algebraic_geometry.res_apply AlgebraicGeometry.Proj.res_apply

@[ext] theorem Proj.ext (h : s.1 = t.1) : s = t := Subtype.ext h
@[simp] theorem Proj.add_apply : (s + t).1 x = s.1 x + t.1 x := rfl
@[simp] theorem Proj.mul_apply : (s * t).1 x = s.1 x * t.1 x := rfl
@[simp] theorem Proj.sub_apply : (s - t).1 x = s.1 x - t.1 x := rfl
@[simp] theorem Proj.pow_apply (n : â„•) : (s ^ n).1 x = s.1 x ^ n := rfl
@[simp] theorem Proj.zero_apply : (0 : (Proj.structureSheaf ğ’œ).1.obj V).1 x = 0 := rfl
@[simp] theorem Proj.one_apply : (1 : (Proj.structureSheaf ğ’œ).1.obj V).1 x = 1 := rfl

end

/-- `Proj` of a graded ring as a `SheafedSpace`-/
def Proj.toSheafedSpace : SheafedSpace CommRingCat where
  carrier := TopCat.of (ProjectiveSpectrum ğ’œ)
  presheaf := (Proj.structureSheaf ğ’œ).1
  IsSheaf := (Proj.structureSheaf ğ’œ).2
set_option linter.uppercaseLean3 false in
#align algebraic_geometry.Proj.to_SheafedSpace AlgebraicGeometry.Proj.toSheafedSpace

/-- The ring homomorphism that takes a section of the structure sheaf of `Proj` on the open set `U`,
implemented as a subtype of dependent functions to localizations at homogeneous prime ideals, and
evaluates the section on the point corresponding to a given homogeneous prime ideal. -/
def openToLocalization (U : Opens (ProjectiveSpectrum.top ğ’œ)) (x : ProjectiveSpectrum.top ğ’œ)
    (hx : x âˆˆ U) : (Proj.structureSheaf ğ’œ).1.obj (op U) âŸ¶ CommRingCat.of (at x) where
  toFun s := (s.1 âŸ¨x, hxâŸ© : _)
  map_one' := rfl
  map_mul' _ _ := rfl
  map_zero' := rfl
  map_add' _ _ := rfl
#align algebraic_geometry.open_to_localization AlgebraicGeometry.openToLocalization

/-- The ring homomorphism from the stalk of the structure sheaf of `Proj` at a point corresponding
to a homogeneous prime ideal `x` to the *homogeneous localization* at `x`,
formed by gluing the `openToLocalization` maps. -/
def stalkToFiberRingHom (x : ProjectiveSpectrum.top ğ’œ) :
    (Proj.structureSheaf ğ’œ).presheaf.stalk x âŸ¶ CommRingCat.of (at x) :=
  Limits.colimit.desc ((OpenNhds.inclusion x).op â‹™ (Proj.structureSheaf ğ’œ).1)
    { pt := _
      Î¹ :=
        { app := fun U =>
            openToLocalization ğ’œ ((OpenNhds.inclusion _).obj U.unop) x U.unop.2 } }
#align algebraic_geometry.stalk_to_fiber_ring_hom AlgebraicGeometry.stalkToFiberRingHom

@[simp]
theorem germ_comp_stalkToFiberRingHom (U : Opens (ProjectiveSpectrum.top ğ’œ)) (x : U) :
    (Proj.structureSheaf ğ’œ).presheaf.germ x â‰« stalkToFiberRingHom ğ’œ x =
      openToLocalization ğ’œ U x x.2 :=
  Limits.colimit.Î¹_desc _ _
#align algebraic_geometry.germ_comp_stalk_to_fiber_ring_hom AlgebraicGeometry.germ_comp_stalkToFiberRingHom

@[simp]
theorem stalkToFiberRingHom_germ' (U : Opens (ProjectiveSpectrum.top ğ’œ))
    (x : ProjectiveSpectrum.top ğ’œ) (hx : x âˆˆ U) (s : (Proj.structureSheaf ğ’œ).1.obj (op U)) :
    stalkToFiberRingHom ğ’œ x ((Proj.structureSheaf ğ’œ).presheaf.germ âŸ¨x, hxâŸ© s) = (s.1 âŸ¨x, hxâŸ© : _) :=
  RingHom.ext_iff.1 (germ_comp_stalkToFiberRingHom ğ’œ U âŸ¨x, hxâŸ© : _) s
#align algebraic_geometry.stalk_to_fiber_ring_hom_germ' AlgebraicGeometry.stalkToFiberRingHom_germ'

@[simp]
theorem stalkToFiberRingHom_germ (U : Opens (ProjectiveSpectrum.top ğ’œ)) (x : U)
    (s : (Proj.structureSheaf ğ’œ).1.obj (op U)) :
    stalkToFiberRingHom ğ’œ x ((Proj.structureSheaf ğ’œ).presheaf.germ x s) = s.1 x :=
  stalkToFiberRingHom_germ' ğ’œ U _ _ _
#align algebraic_geometry.stalk_to_fiber_ring_hom_germ AlgebraicGeometry.stalkToFiberRingHom_germ

theorem mem_basicOpen_den (x : ProjectiveSpectrum.top ğ’œ)
    (f : HomogeneousLocalization.NumDenSameDeg ğ’œ x.asHomogeneousIdeal.toIdeal.primeCompl) :
    x âˆˆ ProjectiveSpectrum.basicOpen ğ’œ f.den := by
  rw [ProjectiveSpectrum.mem_basicOpen]
  exact f.den_mem
#align algebraic_geometry.homogeneous_localization.mem_basic_open AlgebraicGeometry.mem_basicOpen_den

/-- Given a point `x` corresponding to a homogeneous prime ideal, there is a (dependent) function
such that, for any `f` in the homogeneous localization at `x`, it returns the obvious section in the
basic open set `D(f.den)`-/
def sectionInBasicOpen (x : ProjectiveSpectrum.top ğ’œ) :
    âˆ€ f : HomogeneousLocalization.NumDenSameDeg ğ’œ x.asHomogeneousIdeal.toIdeal.primeCompl,
    (Proj.structureSheaf ğ’œ).1.obj (op (ProjectiveSpectrum.basicOpen ğ’œ f.den)) :=
  fun f =>
  âŸ¨fun y => HomogeneousLocalization.mk âŸ¨f.deg, f.num, f.den, y.2âŸ©, fun y =>
    âŸ¨ProjectiveSpectrum.basicOpen ğ’œ f.den, y.2,
      âŸ¨ğŸ™ _, âŸ¨f.deg, âŸ¨f.num, f.den, _, fun _ => rflâŸ©âŸ©âŸ©âŸ©âŸ©
#align algebraic_geometry.section_in_basic_open AlgebraicGeometry.sectionInBasicOpen

open HomogeneousLocalization in
/-- Given any point `x` and `f` in the homogeneous localization at `x`, there is an element in the
stalk at `x` obtained by `sectionInBasicOpen`. This is the inverse of `stalkToFiberRingHom`.
-/
def homogeneousLocalizationToStalk (x : ProjectiveSpectrum.top ğ’œ) (y : at x) :
    (Proj.structureSheaf ğ’œ).presheaf.stalk x := Quotient.liftOn' y (fun f =>
  (Proj.structureSheaf ğ’œ).presheaf.germ âŸ¨x, mem_basicOpen_den _ x fâŸ© (sectionInBasicOpen _ x f))
  fun f g (e : f.embedding = g.embedding) â†¦ by
    simp only [HomogeneousLocalization.NumDenSameDeg.embedding, Localization.mk_eq_mk',
      IsLocalization.mk'_eq_iff_eq,
      IsLocalization.eq_iff_exists x.asHomogeneousIdeal.toIdeal.primeCompl] at e
    obtain âŸ¨âŸ¨c, hcâŸ©, hc'âŸ© := e
    apply (Proj.structureSheaf ğ’œ).presheaf.germ_ext
      (ProjectiveSpectrum.basicOpen ğ’œ f.den.1 âŠ“
        ProjectiveSpectrum.basicOpen ğ’œ g.den.1 âŠ“ ProjectiveSpectrum.basicOpen ğ’œ c)
      âŸ¨âŸ¨mem_basicOpen_den _ x f, mem_basicOpen_den _ x gâŸ©, hcâŸ©
      (homOfLE inf_le_left â‰« homOfLE inf_le_left) (homOfLE inf_le_left â‰« homOfLE inf_le_right)
    apply Subtype.ext
    ext âŸ¨t, âŸ¨htf, htgâŸ©, ht'âŸ©
    rw [Proj.res_apply, Proj.res_apply]
    simp only [sectionInBasicOpen, HomogeneousLocalization.val_mk, Localization.mk_eq_mk',
      IsLocalization.mk'_eq_iff_eq]
    apply (IsLocalization.map_units (M := t.asHomogeneousIdeal.toIdeal.primeCompl)
      (Localization t.asHomogeneousIdeal.toIdeal.primeCompl) âŸ¨c, ht'âŸ©).mul_left_cancel
    rw [â† map_mul, â† map_mul, hc']
#align algebraic_geometry.homogeneous_localization_to_stalk AlgebraicGeometry.homogeneousLocalizationToStalk

lemma homogeneousLocalizationToStalk_stalkToFiberRingHom (x z) :
    homogeneousLocalizationToStalk ğ’œ x (stalkToFiberRingHom ğ’œ x z) = z := by
  obtain âŸ¨U, hxU, s, rflâŸ© := (Proj.structureSheaf ğ’œ).presheaf.germ_exist x z
  obtain âŸ¨V, hxV, i, n, a, b, h, eâŸ© := s.2 âŸ¨x, hxUâŸ©
  simp only at e
  rw [stalkToFiberRingHom_germ', homogeneousLocalizationToStalk, e âŸ¨x, hxVâŸ©, Quotient.liftOn'_mk'']
  refine Presheaf.germ_ext _ V hxV (by exact homOfLE <| fun _ h' â†¦ h âŸ¨_, h'âŸ©) i ?_
  apply Subtype.ext
  ext âŸ¨t, htâŸ©
  rw [Proj.res_apply, Proj.res_apply]
  simp only [sectionInBasicOpen, HomogeneousLocalization.val_mk, Localization.mk_eq_mk',
    IsLocalization.mk'_eq_iff_eq, e âŸ¨t, htâŸ©]

lemma stalkToFiberRingHom_homogeneousLocalizationToStalk (x z) :
    stalkToFiberRingHom ğ’œ x (homogeneousLocalizationToStalk ğ’œ x z) = z := by
  obtain âŸ¨z, rflâŸ© := Quotient.surjective_Quotient_mk'' z
  rw [homogeneousLocalizationToStalk, Quotient.liftOn'_mk'',
    stalkToFiberRingHom_germ', sectionInBasicOpen]

/-- Using `homogeneousLocalizationToStalk`, we construct a ring isomorphism between stalk at `x`
and homogeneous localization at `x` for any point `x` in `Proj`. -/
def Proj.stalkIso' (x : ProjectiveSpectrum.top ğ’œ) :
    (Proj.structureSheaf ğ’œ).presheaf.stalk x â‰ƒ+* at x where
  __ := stalkToFiberRingHom _ x
  invFun := homogeneousLocalizationToStalk ğ’œ x
  left_inv := homogeneousLocalizationToStalk_stalkToFiberRingHom ğ’œ x
  right_inv := stalkToFiberRingHom_homogeneousLocalizationToStalk ğ’œ x
set_option linter.uppercaseLean3 false in
#align algebraic_geometry.Proj.stalk_iso' AlgebraicGeometry.Proj.stalkIso'

@[simp]
theorem Proj.stalkIso'_germ' (U : Opens (ProjectiveSpectrum.top ğ’œ))
    (x : ProjectiveSpectrum.top ğ’œ) (hx : x âˆˆ U) (s : (Proj.structureSheaf ğ’œ).1.obj (op U)) :
    Proj.stalkIso' ğ’œ x ((Proj.structureSheaf ğ’œ).presheaf.germ âŸ¨x, hxâŸ© s) = (s.1 âŸ¨x, hxâŸ© : _) :=
  stalkToFiberRingHom_germ' ğ’œ U x hx s

@[simp]
theorem Proj.stalkIso'_germ (U : Opens (ProjectiveSpectrum.top ğ’œ)) (x : U)
    (s : (Proj.structureSheaf ğ’œ).1.obj (op U)) :
    Proj.stalkIso' ğ’œ x ((Proj.structureSheaf ğ’œ).presheaf.germ x s) = s.1 x :=
  stalkToFiberRingHom_germ' ğ’œ U x x.2 s

@[simp]
theorem Proj.stalkIso'_symm_mk (x) (f) :
    (Proj.stalkIso' ğ’œ x).symm (.mk f) = (Proj.structureSheaf ğ’œ).presheaf.germ
      âŸ¨x, mem_basicOpen_den _ x fâŸ© (sectionInBasicOpen _ x f) := rfl

/-- `Proj` of a graded ring as a `LocallyRingedSpace`-/
def Proj.toLocallyRingedSpace : LocallyRingedSpace :=
  { Proj.toSheafedSpace ğ’œ with
    localRing := fun x =>
      @RingEquiv.localRing _ _ _ (show LocalRing (at x) from inferInstance) _
        (Proj.stalkIso' ğ’œ x).symm }
set_option linter.uppercaseLean3 false in
#align algebraic_geometry.Proj.to_LocallyRingedSpace AlgebraicGeometry.Proj.toLocallyRingedSpace

end

end AlgebraicGeometry
