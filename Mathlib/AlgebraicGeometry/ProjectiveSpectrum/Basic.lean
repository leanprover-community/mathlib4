/-
Copyright (c) 2024 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.ProjectiveSpectrum.Scheme
import Mathlib.AlgebraicGeometry.AffineScheme
import Mathlib.AlgebraicGeometry.Gluing

/-!

# Basic properties of the scheme `Proj A`

The scheme `Proj ğ’œ` for a graded algebra `ğ’œ` is constructed in
`AlgebraicGeometry/ProjectiveSpectrum/Scheme.lean`.
In this file we provide basic properties of the scheme.

## Main results
- `AlgebraicGeometry.Proj.toSpecZero`: The structure map `Proj A âŸ¶ Spec (A 0)`.
- `AlgebraicGeometry.Proj.basicOpenIsoSpec`:
  The canonical isomorphism `Proj A |_ Dâ‚Š(f) â‰… Spec (A_f)â‚€`
  when `f` is homogeneous of positive degree.
- `AlgebraicGeometry.Proj.awayÎ¹`: The open immersion `Spec (A_f)â‚€ âŸ¶ Proj A`.
- `AlgebraicGeometry.Proj.affineOpenCover`: The open cover of `Proj A` by `Spec (A_f)â‚€` for all
  homogeneous `f` of positive degree.
- `AlgebraicGeometry.Proj.stalkIso`:
  The stalk of `Proj A` at `x` is the degree `0` part of the localization of `A` at `x`.
- `AlgebraicGeometry.Proj.fromOfGlobalSections`:
  Given a map `f : A â†’+* Î“(X, âŠ¤)` such that the image of the irrelevant ideal under `f`
  generates the whole ring, we can construct a map `X âŸ¶ Proj ğ’œ`.

-/

namespace AlgebraicGeometry.Proj

open HomogeneousLocalization CategoryTheory

universe u

variable {R : Type*} {A : Type u}
variable [CommRing R] [CommRing A] [Algebra R A]
variable (ğ’œ : â„• â†’ Submodule R A)
variable [GradedAlgebra ğ’œ]

section basicOpen

variable (f g : A)

/-- The basic open set `Dâ‚Š(f)` associated to `f : A`. -/
def basicOpen : (Proj ğ’œ).Opens :=
  ProjectiveSpectrum.basicOpen ğ’œ f

@[simp]
theorem mem_basicOpen (x : Proj ğ’œ) :
    x âˆˆ basicOpen ğ’œ f â†” f âˆ‰ x.asHomogeneousIdeal :=
  Iff.rfl

@[simp] theorem basicOpen_one : basicOpen ğ’œ 1 = âŠ¤ := ProjectiveSpectrum.basicOpen_one ..

@[simp] theorem basicOpen_zero : basicOpen ğ’œ 0 = âŠ¥ := ProjectiveSpectrum.basicOpen_zero ..

@[simp] theorem basicOpen_pow (n) (hn : 0 < n) : basicOpen ğ’œ (f ^ n) = basicOpen ğ’œ f :=
  ProjectiveSpectrum.basicOpen_pow ğ’œ f n hn

theorem basicOpen_mul : basicOpen ğ’œ (f * g) = basicOpen ğ’œ f âŠ“ basicOpen ğ’œ g :=
  ProjectiveSpectrum.basicOpen_mul ..

theorem basicOpen_mono (hfg : f âˆ£ g) : basicOpen ğ’œ g â‰¤ basicOpen ğ’œ f :=
  (hfg.choose_spec â–¸ basicOpen_mul ğ’œ f _).trans_le inf_le_left

theorem basicOpen_eq_iSup_proj (f : A) :
    basicOpen ğ’œ f = â¨† i : â„•, basicOpen ğ’œ (GradedAlgebra.proj ğ’œ i f) :=
  ProjectiveSpectrum.basicOpen_eq_union_of_projection ..

theorem isBasis_basicOpen :
    TopologicalSpace.Opens.IsBasis (Set.range (basicOpen ğ’œ)) := by
  delta TopologicalSpace.Opens.IsBasis
  convert ProjectiveSpectrum.isTopologicalBasis_basic_opens ğ’œ
  exact (Set.range_comp _ _).symm

/-- If `{ xáµ¢ }` spans the irrelevant ideal of `A`, then `Dâ‚Š(xáµ¢)` covers `Proj A`. -/
lemma iSup_basicOpen_eq_top {Î¹ : Type*} (f : Î¹ â†’ A)
    (hf : (HomogeneousIdeal.irrelevant ğ’œ).toIdeal â‰¤ Ideal.span (Set.range f)) :
    â¨† i, Proj.basicOpen ğ’œ (f i) = âŠ¤ := by
  classical
  refine top_le_iff.mp fun x hx â†¦ TopologicalSpace.Opens.mem_iSup.mpr ?_
  by_contra! H
  simp only [mem_basicOpen, Decidable.not_not] at H
  refine x.not_irrelevant_le (hf.trans ?_)
  rwa [Ideal.span_le, Set.range_subset_iff]

/-- If `{ xáµ¢ }` are homogeneous and span `A` as an `Aâ‚€` algebra, then `Dâ‚Š(xáµ¢)` covers `Proj A`. -/
lemma iSup_basicOpen_eq_top' {Î¹ : Type*} (f : Î¹ â†’ A)
    (hfn : âˆ€ i, âˆƒ n, f i âˆˆ ğ’œ n)
    (hf : Algebra.adjoin (ğ’œ 0) (Set.range f) = âŠ¤) :
    â¨† i, Proj.basicOpen ğ’œ (f i) = âŠ¤ := by
  classical
  apply Proj.iSup_basicOpen_eq_top
  intro x hx
  convert_to x - GradedRing.projZeroRingHom ğ’œ x âˆˆ _
  Â· rw [GradedRing.projZeroRingHom_apply, â† GradedRing.proj_apply,
      (HomogeneousIdeal.mem_irrelevant_iff _ _).mp hx, sub_zero]
  clear hx
  have := (eq_iff_iff.mp congr(x âˆˆ $hf)).mpr trivial
  induction this using Algebra.adjoin_induction with
  | mem x hx =>
    obtain âŸ¨i, rflâŸ© := hx
    obtain âŸ¨n, hnâŸ© := hfn i
    rw [GradedRing.projZeroRingHom_apply]
    by_cases hn' : n = 0
    Â· rw [DirectSum.decompose_of_mem_same ğ’œ (hn' â–¸ hn), sub_self]
      exact zero_mem _
    Â· rw [DirectSum.decompose_of_mem_ne ğ’œ hn hn', sub_zero]
      exact Ideal.subset_span âŸ¨_, rflâŸ©
  | algebraMap r =>
    convert zero_mem (Ideal.span _)
    rw [sub_eq_zero]
    exact (DirectSum.decompose_of_mem_same ğ’œ r.2).symm
  | add x y hx hy _ _ =>
    rw [map_add, add_sub_add_comm]
    exact add_mem â€¹_â€º â€¹_â€º
  | mul x y hx hy hx' hy' =>
    convert add_mem (Ideal.mul_mem_left _ x hy')
      (Ideal.mul_mem_right (GradedRing.projZeroRingHom ğ’œ y) _ hx') using 1
    rw [map_mul]
    ring

/-- The canonical map `(A_f)â‚€ âŸ¶ Î“(Proj A, Dâ‚Š(f))`.
This is an isomorphism when `f` is homogeneous of positive degree. See `basicOpenIsoAway` below. -/
def awayToSection : CommRingCat.of (Away ğ’œ f) âŸ¶ Î“(Proj ğ’œ, basicOpen ğ’œ f) :=
  ProjectiveSpectrum.Proj.awayToSection ..

/-- The canonical map `Proj A |_ Dâ‚Š(f) âŸ¶ Spec (A_f)â‚€`.
This is an isomorphism when `f` is homogeneous of positive degree. See `basicOpenIsoSpec` below. -/
noncomputable
def basicOpenToSpec : (basicOpen ğ’œ f).toScheme âŸ¶ Spec (.of <| Away ğ’œ f) :=
  (basicOpen ğ’œ f).toSpecÎ“ â‰« Spec.map (awayToSection ğ’œ f)

lemma basicOpenToSpec_app_top :
    (basicOpenToSpec ğ’œ f).app âŠ¤ = (Scheme.Î“SpecIso _).hom â‰« awayToSection ğ’œ f â‰«
      (basicOpen ğ’œ f).topIso.inv := by
  rw [basicOpenToSpec]
  simp only [Scheme.Hom.comp_base, TopologicalSpace.Opens.map_comp_obj,
    TopologicalSpace.Opens.map_top, Scheme.Hom.comp_app, Scheme.Opens.topIso_inv, eqToHom_op]
  erw [Scheme.Hom.comp_app]
  simp

/-- The structure map `Proj A âŸ¶ Spec Aâ‚€`. -/
noncomputable
def toSpecZero : Proj ğ’œ âŸ¶ Spec (.of <| ğ’œ 0) :=
  (Scheme.topIso _).inv â‰« (Scheme.isoOfEq _ (basicOpen_one _)).inv â‰«
    basicOpenToSpec ğ’œ 1 â‰« Spec.map (CommRingCat.ofHom (fromZeroRingHom ğ’œ _))

variable {m} (f_deg : f âˆˆ ğ’œ m) (hm : 0 < m)

/-- The canonical isomorphism `Proj A |_ Dâ‚Š(f) â‰… Spec (A_f)â‚€`
when `f` is homogeneous of positive degree. -/
@[simps! -isSimp hom]
noncomputable
def basicOpenIsoSpec : (basicOpen ğ’œ f).toScheme â‰… Spec (.of <| Away ğ’œ f) :=
  have : IsIso (basicOpenToSpec ğ’œ f) := by
    apply (isIso_iff_of_reflects_iso _ Scheme.forgetToLocallyRingedSpace).mp ?_
    convert ProjectiveSpectrum.Proj.isIso_toSpec ğ’œ f f_deg hm using 1
    refine Eq.trans ?_ (Î“Spec.locallyRingedSpaceAdjunction.homEquiv_apply _ _ _).symm
    dsimp [basicOpenToSpec, Scheme.Opens.toSpecÎ“]
    simp only [eqToHom_op, Category.assoc, â† Spec.map_comp]
    rfl
  asIso (basicOpenToSpec ğ’œ f)

/-- The canonical isomorphism `(A_f)â‚€ â‰… Î“(Proj A, Dâ‚Š(f))`
when `f` is homogeneous of positive degree. -/
@[simps! -isSimp hom]
noncomputable
def basicOpenIsoAway : CommRingCat.of (Away ğ’œ f) â‰… Î“(Proj ğ’œ, basicOpen ğ’œ f) :=
  have : IsIso (awayToSection ğ’œ f) := by
    have := basicOpenToSpec_app_top ğ’œ f
    rw [â† Iso.inv_comp_eq, Iso.eq_comp_inv] at this
    rw [â† this, â† basicOpenIsoSpec_hom ğ’œ f f_deg hm]
    infer_instance
  asIso (awayToSection ğ’œ f)

/-- The open immersion `Spec (A_f)â‚€ âŸ¶ Proj A`. -/
noncomputable
def awayÎ¹ : Spec (.of <| Away ğ’œ f) âŸ¶ Proj ğ’œ :=
  (basicOpenIsoSpec ğ’œ f f_deg hm).inv â‰« (Proj.basicOpen ğ’œ f).Î¹

@[reassoc]
lemma basicOpenIsoSpec_inv_Î¹ :
    (basicOpenIsoSpec ğ’œ f f_deg hm).inv â‰« (Proj.basicOpen ğ’œ f).Î¹ = awayÎ¹ ğ’œ f f_deg hm := rfl

instance : IsOpenImmersion (Proj.awayÎ¹ ğ’œ f f_deg hm) :=
  IsOpenImmersion.comp _ _

lemma opensRange_awayÎ¹ :
    (Proj.awayÎ¹ ğ’œ f f_deg hm).opensRange = Proj.basicOpen ğ’œ f :=
  (Scheme.Hom.opensRange_comp_of_isIso _ _).trans (basicOpen ğ’œ f).opensRange_Î¹

include f_deg hm in
lemma isAffineOpen_basicOpen : IsAffineOpen (basicOpen ğ’œ f) := by
  rw [â† opensRange_awayÎ¹ ğ’œ f f_deg hm]
  exact isAffineOpen_opensRange (awayÎ¹ _ _ _ _)

@[reassoc]
lemma awayÎ¹_toSpecZero : awayÎ¹ ğ’œ f f_deg hm â‰« toSpecZero ğ’œ =
    Spec.map (CommRingCat.ofHom (fromZeroRingHom ğ’œ _)) := by
  rw [toSpecZero, basicOpenToSpec, awayÎ¹]
  simp only [Category.assoc, Iso.inv_comp_eq, basicOpenIsoSpec_hom]
  have (U) (e : U = âŠ¤) : (basicOpen ğ’œ f).Î¹ â‰« (Scheme.topIso _).inv â‰« (Scheme.isoOfEq _ e).inv =
      Scheme.homOfLE _ (le_top.trans_eq e.symm) := by
    simp only [â† Category.assoc, Iso.comp_inv_eq]
    simp only [Scheme.topIso_hom, Category.assoc, Scheme.isoOfEq_hom_Î¹, Scheme.homOfLE_Î¹]
  rw [reassoc_of% this, â† Scheme.Opens.toSpecÎ“_SpecMap_presheaf_map_assoc, basicOpenToSpec,
    Category.assoc, â† Spec.map_comp, â† Spec.map_comp, â† Spec.map_comp]
  rfl

variable {f}
variable {m' : â„•} {g : A} (g_deg : g âˆˆ ğ’œ m') (hm' : 0 < m') {x : A} (hx : x = f * g)

@[reassoc]
lemma awayMap_awayToSection :
    CommRingCat.ofHom (awayMap ğ’œ g_deg hx) â‰« awayToSection ğ’œ x =
      awayToSection ğ’œ f â‰« (Proj ğ’œ).presheaf.map (homOfLE (basicOpen_mono _ _ _ âŸ¨_, hxâŸ©)).op := by
  ext a
  apply Subtype.ext
  ext âŸ¨i, hiâŸ©
  obtain âŸ¨âŸ¨n, a, âŸ¨b, hb'âŸ©, i, rfl : _ = bâŸ©, rflâŸ© := mk_surjective a
  simp only [homOfLE_leOfHom, CommRingCat.hom_comp, RingHom.coe_comp, Function.comp_apply]
  erw [ProjectiveSpectrum.Proj.awayToSection_apply]
  rw [CommRingCat.hom_ofHom, val_awayMap_mk, Localization.mk_eq_mk', IsLocalization.map_mk',
    â† Localization.mk_eq_mk']
  refine Localization.mk_eq_mk_iff.mpr ?_
  rw [Localization.r_iff_exists]
  use 1
  simp only [OneMemClass.coe_one, RingHom.id_apply, one_mul, hx]
  ring

@[reassoc]
lemma basicOpenToSpec_SpecMap_awayMap :
    basicOpenToSpec ğ’œ x â‰« Spec.map (CommRingCat.ofHom (awayMap ğ’œ g_deg hx)) =
      (Proj ğ’œ).homOfLE (basicOpen_mono _ _ _ âŸ¨_, hxâŸ©) â‰« basicOpenToSpec ğ’œ f := by
  rw [basicOpenToSpec, Category.assoc, â† Spec.map_comp, awayMap_awayToSection,
    Spec.map_comp, Scheme.Opens.toSpecÎ“_SpecMap_presheaf_map_assoc]
  rfl

@[reassoc]
lemma SpecMap_awayMap_awayÎ¹ :
    Spec.map (CommRingCat.ofHom (awayMap ğ’œ g_deg hx)) â‰« awayÎ¹ ğ’œ f f_deg hm =
      awayÎ¹ ğ’œ x (hx â–¸ SetLike.mul_mem_graded f_deg g_deg) (hm.trans_le (m.le_add_right m')) := by
  rw [awayÎ¹, awayÎ¹, Iso.eq_inv_comp, basicOpenIsoSpec_hom, basicOpenToSpec_SpecMap_awayMap_assoc,
  â† basicOpenIsoSpec_hom _ _ f_deg hm, Iso.hom_inv_id_assoc, Scheme.homOfLE_Î¹]

/-- The isomorphism `Dâ‚Š(f) Ã—[Proj ğ’œ] Dâ‚Š(g) â‰… Dâ‚Š(fg)`. -/
noncomputable
def pullbackAwayÎ¹Iso :
    Limits.pullback (awayÎ¹ ğ’œ f f_deg hm) (awayÎ¹ ğ’œ g g_deg hm') â‰… Spec (.of <| Away ğ’œ x) :=
    IsOpenImmersion.isoOfRangeEq (Limits.pullback.fst _ _ â‰« awayÎ¹ ğ’œ f f_deg hm)
      (awayÎ¹ ğ’œ x (hx â–¸ SetLike.mul_mem_graded f_deg g_deg) (hm.trans_le (m.le_add_right m'))) <| by
  rw [IsOpenImmersion.range_pullback_to_base_of_left]
  change ((awayÎ¹ ğ’œ f _ _).opensRange âŠ“ (awayÎ¹ ğ’œ g _ _).opensRange).1 = (awayÎ¹ ğ’œ _ _ _).opensRange.1
  rw [opensRange_awayÎ¹, opensRange_awayÎ¹, opensRange_awayÎ¹, â† basicOpen_mul, hx]

@[reassoc (attr := simp)]
lemma pullbackAwayÎ¹Iso_hom_awayÎ¹ :
    (pullbackAwayÎ¹Iso ğ’œ f_deg hm g_deg hm' hx).hom â‰«
      awayÎ¹ ğ’œ x (hx â–¸ SetLike.mul_mem_graded f_deg g_deg) (hm.trans_le (m.le_add_right m')) =
      Limits.pullback.fst _ _ â‰« awayÎ¹ ğ’œ f f_deg hm :=
  IsOpenImmersion.isoOfRangeEq_hom_fac ..

@[reassoc (attr := simp)]
lemma pullbackAwayÎ¹Iso_hom_SpecMap_awayMap_left :
    (pullbackAwayÎ¹Iso ğ’œ f_deg hm g_deg hm' hx).hom â‰«
      Spec.map (CommRingCat.ofHom (awayMap ğ’œ g_deg hx)) = Limits.pullback.fst _ _ := by
  rw [â† cancel_mono (awayÎ¹ ğ’œ f f_deg hm), â† pullbackAwayÎ¹Iso_hom_awayÎ¹,
    Category.assoc, SpecMap_awayMap_awayÎ¹]

@[reassoc (attr := simp)]
lemma pullbackAwayÎ¹Iso_hom_SpecMap_awayMap_right :
    (pullbackAwayÎ¹Iso ğ’œ f_deg hm g_deg hm' hx).hom â‰«
      Spec.map (CommRingCat.ofHom (awayMap ğ’œ f_deg (hx.trans (mul_comm _ _)))) =
      Limits.pullback.snd _ _ := by
  rw [â† cancel_mono (awayÎ¹ ğ’œ g g_deg hm'), â† Limits.pullback.condition,
    â† pullbackAwayÎ¹Iso_hom_awayÎ¹ ğ’œ f_deg hm g_deg hm' hx,
    Category.assoc, SpecMap_awayMap_awayÎ¹]
  rfl

@[reassoc (attr := simp)]
lemma pullbackAwayÎ¹Iso_inv_fst :
    (pullbackAwayÎ¹Iso ğ’œ f_deg hm g_deg hm' hx).inv â‰« Limits.pullback.fst _ _ =
      Spec.map (CommRingCat.ofHom (awayMap ğ’œ g_deg hx)) := by
  rw [â† pullbackAwayÎ¹Iso_hom_SpecMap_awayMap_left, Iso.inv_hom_id_assoc]

@[reassoc (attr := simp)]
lemma pullbackAwayÎ¹Iso_inv_snd :
    (pullbackAwayÎ¹Iso ğ’œ f_deg hm g_deg hm' hx).inv â‰« Limits.pullback.snd _ _ =
      Spec.map (CommRingCat.ofHom (awayMap ğ’œ f_deg (hx.trans (mul_comm _ _)))) := by
  rw [â† pullbackAwayÎ¹Iso_hom_SpecMap_awayMap_right (hx := hx) .., Iso.inv_hom_id_assoc]

include hm' in
lemma awayÎ¹_preimage_basicOpen :
    awayÎ¹ ğ’œ f f_deg hm â»Â¹áµ basicOpen ğ’œ g =
      PrimeSpectrum.basicOpen (Away.isLocalizationElem f_deg g_deg) := by
  ext1
  trans Set.range (Spec.map (CommRingCat.ofHom (awayMap ğ’œ g_deg rfl))).base
  Â· rw [â† pullbackAwayÎ¹Iso_inv_fst ğ’œ f_deg hm g_deg hm' rfl]
    simp only [TopologicalSpace.Opens.map_coe, Scheme.Hom.comp_base,
      TopCat.hom_comp, ContinuousMap.coe_comp, Set.range_comp]
    rw [Set.range_eq_univ.mpr (by exact
      (pullbackAwayÎ¹Iso ğ’œ f_deg hm g_deg hm' rfl).inv.homeomorph.surjective),
      â† opensRange_awayÎ¹ _ _ g_deg hm']
    simp [IsOpenImmersion.range_pullbackFst]
  Â· letI := (awayMap (f := f) ğ’œ g_deg rfl).toAlgebra
    letI := HomogeneousLocalization.Away.isLocalization_mul f_deg g_deg rfl hm.ne'
    exact PrimeSpectrum.localization_away_comap_range _ _

open TopologicalSpace.Opens in
/-- Given a family of homogeneous elements `f` of positive degree that spans the irrelevant ideal,
`Spec (A_f)â‚€ âŸ¶ Proj A` forms an affine open cover of `Proj A`. -/
noncomputable
def affineOpenCoverOfIrrelevantLESpan {Î¹ : Type*} (f : Î¹ â†’ A) {m : Î¹ â†’ â„•}
    (f_deg : âˆ€ i, f i âˆˆ ğ’œ (m i)) (hm : âˆ€ i, 0 < m i)
    (hf : (HomogeneousIdeal.irrelevant ğ’œ).toIdeal â‰¤ Ideal.span (Set.range f)) :
    (Proj ğ’œ).AffineOpenCover where
  Iâ‚€ := Î¹
  X i := .of (Away ğ’œ (f i))
  f i := awayÎ¹ ğ’œ (f i) (f_deg i) (hm i)
  idx x := (mem_iSup.mp ((iSup_basicOpen_eq_top ğ’œ f hf).ge (Set.mem_univ x))).choose
  covers x := by
    change x âˆˆ (awayÎ¹ ğ’œ _ _ _).opensRange
    rw [opensRange_awayÎ¹]
    exact (mem_iSup.mp ((iSup_basicOpen_eq_top ğ’œ f hf).ge (Set.mem_univ x))).choose_spec

@[deprecated (since := "2025-10-07")]
noncomputable alias openCoverOfISupEqTop := affineOpenCoverOfIrrelevantLESpan

/-- `Proj A` is covered by `Spec (A_f)â‚€` for all homogeneous elements of positive degree. -/
noncomputable
def affineOpenCover : (Proj ğ’œ).AffineOpenCover :=
  affineOpenCoverOfIrrelevantLESpan ğ’œ
    (Î¹ := Î£ i : PNat, ğ’œ i) (m := fun i â†¦ i.1) (fun i â†¦ i.2) (fun i â†¦ i.2.2) (fun i â†¦ i.1.2) <| by
  classical
  intro z hz
  rw [â† DirectSum.sum_support_decompose ğ’œ z]
  refine Ideal.sum_mem _ fun c hc â†¦ if hc0 : c = 0 then ?_ else
    Ideal.subset_span âŸ¨âŸ¨âŸ¨c, Nat.pos_iff_ne_zero.mpr hc0âŸ©, _âŸ©, rflâŸ©
  convert Ideal.zero_mem _
  subst hc0
  exact hz

end basicOpen

section stalk

/-- The stalk of `Proj A` at `x` is the degree `0` part of the localization of `A` at `x`. -/
noncomputable
def stalkIso (x : Proj ğ’œ) :
    (Proj ğ’œ).presheaf.stalk x â‰… .of (AtPrime ğ’œ x.asHomogeneousIdeal.toIdeal) :=
  (stalkIso' ğ’œ x).toCommRingCatIso

end stalk

noncomputable section ofGlobalSection

open Limits

variable {X : Scheme.{u}} (f : A â†’+* Î“(X, âŠ¤)) {x x' : Î“(X, âŠ¤)} {t t' : A} {d d' : â„•}

/-- Given a graded ring `A` and a map `f : A â†’+* Î“(X, âŠ¤)`,
for each homogeneous `t` of positive degree, it induces a map from `D(f(t)) âŸ¶ Dâ‚Š(t)`. -/
def toBasicOpenOfGlobalSections (H : f t = x) (h0d : 0 < d) (hd : t âˆˆ ğ’œ d) :
    (X.basicOpen x).toScheme âŸ¶ basicOpen ğ’œ t := by
  refine ?_ â‰« (basicOpenIsoSpec _ _ hd h0d).inv
  refine (X.isoOfEq (X.toSpecÎ“_preimage_basicOpen x)).inv â‰« X.toSpecÎ“ âˆ£_ _ â‰« ?_
  refine (basicOpenIsoSpecAway _).hom â‰«
    Spec.map (CommRingCat.ofHom (RingHom.comp ?_ (algebraMap _ (Localization.Away t))))
  refine IsLocalization.map (M := .powers t) (T := .powers x) _ f ?_
  Â· rw [â† Submonoid.map_le_iff_le_comap, Submonoid.map_powers]
    simp [H]

@[reassoc]
lemma homOfLE_toBasicOpenOfGlobalSections_Î¹
    {H : f t = x} {h0d : 0 < d} {hd : t âˆˆ ğ’œ d} {H' : f t' = x'} {h0d' : 0 < d'} {hd' : t' âˆˆ ğ’œ d'}
    {s : A} (hts : t * s = t') {n : â„•} (hn : d + n = d') (hs : s âˆˆ ğ’œ n) :
    X.homOfLE (by aesop) â‰« toBasicOpenOfGlobalSections ğ’œ f H h0d hd â‰« (basicOpen ğ’œ t).Î¹ =
      toBasicOpenOfGlobalSections ğ’œ f H' h0d' hd' â‰« (basicOpen ğ’œ t').Î¹ := by
  simp only [toBasicOpenOfGlobalSections, Scheme.isoOfEq_inv,
    â† Scheme.Hom.resLE_eq_morphismRestrict, CommRingCat.ofHom_comp, Spec.map_comp,
    Scheme.Hom.map_resLE_assoc, Category.assoc, basicOpenIsoSpec_inv_Î¹]
  have hx'x : PrimeSpectrum.basicOpen x' â‰¤ PrimeSpectrum.basicOpen x := by
    aesop (add simp PrimeSpectrum.basicOpen_mul)
  rw [â† Scheme.Hom.resLE_map_assoc _ (by simp [X.toSpecÎ“_preimage_basicOpen]) hx'x]
  congr 1
  simp only [â† Iso.inv_comp_eq]
  subst hts hn
  rw [â† SpecMap_awayMap_awayÎ¹ (ğ’œ := ğ’œ) hd h0d
    hs rfl, basicOpenIsoSpecAway_inv_homOfLE_assoc (R := Î“(X, âŠ¤)) x (f s) x' (by simp [â† H', H]),
    Iso.inv_hom_id_assoc]
  simp only [â† Category.assoc, â† Spec.map_comp, â† CommRingCat.ofHom_comp]
  congr 3
  ext
  simp only [RingHom.coe_comp, Function.comp_apply,
    HomogeneousLocalization.algebraMap_apply, HomogeneousLocalization.val_awayMap]
  simp only [â† RingHom.comp_apply]
  congr 1
  apply IsLocalization.ringHom_ext (M := .powers t)
  ext i
  simp [IsLocalization.Away.awayToAwayRight_eq]

variable (f : A â†’+* Î“(X, âŠ¤)) (hf : (HomogeneousIdeal.irrelevant ğ’œ).toIdeal.map f = âŠ¤)

/-- Given a graded ring `A` and a map `f : A â†’+* Î“(X, âŠ¤)` such that the image of the
irrelevant ideal under `f` generates the whole ring, the set of `D(f(r))` for homogeneous `r`
of positive degree forms an open cover on `X`. -/
def openCoverOfMapIrrelevantEqTop : X.OpenCover :=
  X.openCoverOfIsOpenCover (fun ir : Î£' i r, 0 < i âˆ§ r âˆˆ ğ’œ i â†¦
    X.basicOpen (f ir.2.1)) (by
    classical
    have H : Ideal.span (Set.range fun x : Î£' i r, 0 < i âˆ§ r âˆˆ ğ’œ i â†¦ x.2.1) =
        (HomogeneousIdeal.irrelevant ğ’œ).toIdeal := by
      apply le_antisymm
      Â· rw [Ideal.span_le, Set.range_subset_iff]
        rintro âŸ¨i, r, hi0, hriâŸ©
        simp [-ZeroMemClass.coe_eq_zero,
          DirectSum.decompose_of_mem_ne ğ’œ hri hi0.ne']
      Â· intro x hx
        rw [â† DirectSum.sum_support_decompose ğ’œ x]
        refine Ideal.sum_mem _ fun c hc â†¦ ?_
        have : c â‰  0 := by contrapose! hc; simpa [hc] using hx
        exact Ideal.subset_span âŸ¨âŸ¨c, _, this.bot_lt, by simpâŸ©, rflâŸ©
    ext1
    apply compl_injective
    simp only [TopologicalSpace.Opens.coe_iSup, Set.compl_iUnion, â† Scheme.zeroLocus_singleton,
      â† Scheme.zeroLocus_iUnion, Set.iUnion_singleton_eq_range, TopologicalSpace.Opens.coe_top,
      Set.compl_univ]
    rw [â† Scheme.zeroLocus_span, Set.range_comp', â† Ideal.map_span, H, hf]
    simp)

@[deprecated (since := "2025-08-22")] noncomputable alias openCoverOfMapIrreleventEqTop :=
  openCoverOfMapIrrelevantEqTop

/-- Given a graded ring `A` and a map `f : A â†’+* Î“(X, âŠ¤)` such that the image of the
irrelevant ideal under `f` generates the whole ring, we can construct a map `X âŸ¶ Proj ğ’œ`. -/
def fromOfGlobalSections : X âŸ¶ Proj ğ’œ := by
  refine (openCoverOfMapIrrelevantEqTop ğ’œ f hf).glueMorphisms
    (fun ri â†¦ toBasicOpenOfGlobalSections ğ’œ f rfl ri.2.2.1 ri.2.2.2 â‰« Scheme.Opens.Î¹ _) ?_
  rintro x y
  let e : pullback ((openCoverOfMapIrrelevantEqTop ğ’œ f hf).f x)
      ((openCoverOfMapIrrelevantEqTop ğ’œ f hf).f y) â‰… (X.basicOpen (f (x.snd.fst * y.snd.fst))) :=
    (isPullback_opens_inf _ _).isoPullback.symm â‰ªâ‰« X.isoOfEq (by simp)
  rw [â† cancel_epi e.inv]
  trans toBasicOpenOfGlobalSections ğ’œ f rfl (Nat.add_pos_left x.2.2.1 y.1)
    (SetLike.mul_mem_graded x.2.2.2 y.2.2.2) â‰« (Scheme.Opens.Î¹ _)
  Â· simpa [e, openCoverOfMapIrrelevantEqTop, Scheme.isoOfEq_inv] using
      homOfLE_toBasicOpenOfGlobalSections_Î¹ _ _ rfl rfl y.2.2.2
  Â· simpa [e, openCoverOfMapIrrelevantEqTop, Scheme.isoOfEq_inv] using
      (homOfLE_toBasicOpenOfGlobalSections_Î¹ _ _ (mul_comm _ _) (add_comm _ _) x.2.2.2).symm

lemma fromOfGlobalSections_preimage_basicOpen {r : A} {n : â„•} (hn : 0 < n) (hr : r âˆˆ ğ’œ n) :
    fromOfGlobalSections ğ’œ f hf â»Â¹áµ basicOpen ğ’œ r = X.basicOpen (f r) := by
  apply le_antisymm
  Â· intro x hx
    obtain âŸ¨i, x, rflâŸ© := (openCoverOfMapIrrelevantEqTop ğ’œ f hf).exists_eq x
    simp only [TopologicalSpace.Opens.map_coe, Set.mem_preimage, SetLike.mem_coe,
      â† Scheme.Hom.comp_apply, fromOfGlobalSections, Scheme.Cover.Î¹_glueMorphisms] at hx
    simp only [openCoverOfMapIrrelevantEqTop, Scheme.openCoverOfIsOpenCover_X,
      toBasicOpenOfGlobalSections, Scheme.isoOfEq_inv, Category.assoc, basicOpenIsoSpec_inv_Î¹] at hx
    simp only [Scheme.Hom.comp_base, Scheme.homOfLE_base, homOfLE_leOfHom, TopCat.hom_comp,
      ContinuousMap.comp_assoc, ContinuousMap.comp_apply, morphismRestrict_base,
      TopologicalSpace.Opens.carrier_eq_coe] at hx
    rw [â† SetLike.mem_coe, â† Set.mem_preimage, â† TopologicalSpace.Opens.map_coe,
      Proj.awayÎ¹_preimage_basicOpen (ğ’œ := ğ’œ) i.2.2.2 i.2.2.1 hr hn,
      â† Set.mem_preimage, â† TopologicalSpace.Opens.map_coe, â† Function.Injective.mem_set_image
      (Spec.map (CommRingCat.ofHom (algebraMap Î“(X, âŠ¤) _))).isOpenEmbedding.injective,
      â† Scheme.Hom.comp_apply, basicOpenIsoSpecAway, IsOpenImmersion.isoOfRangeEq_hom_fac] at hx
    rw [â† Scheme.toSpecÎ“_preimage_basicOpen, TopologicalSpace.Opens.map_coe, Set.mem_preimage]
    refine Set.mem_of_subset_of_mem (Set.image_subset_iff.mpr ?_) hx
    change PrimeSpectrum.basicOpen _ â‰¤ PrimeSpectrum.basicOpen _
    simp only [CommRingCat.ofHom_comp, CommRingCat.hom_comp, CommRingCat.hom_ofHom,
      RingHom.coe_comp, Function.comp_apply, HomogeneousLocalization.algebraMap_apply,
      HomogeneousLocalization.Away.val_mk, Localization.mk_eq_mk', IsLocalization.map_mk', map_pow,
      PrimeSpectrum.basicOpen_le_basicOpen_iff, IsLocalization.mk'_mem_iff]
    exact Ideal.pow_mem_of_mem _ (Ideal.le_radical (Ideal.mem_span_singleton_self _)) _ i.2.2.1
  Â· intro x hx
    let I : (openCoverOfMapIrrelevantEqTop ğ’œ f hf).Iâ‚€ := âŸ¨n, r, hn, hrâŸ©
    obtain âŸ¨x, rflâŸ© : x âˆˆ ((openCoverOfMapIrrelevantEqTop ğ’œ f hf).f I).opensRange := by
      simpa [openCoverOfMapIrrelevantEqTop] using hx
    simp only [TopologicalSpace.Opens.map_coe, Set.mem_preimage, SetLike.mem_coe,
      â† Scheme.Hom.comp_apply, fromOfGlobalSections, Scheme.Cover.Î¹_glueMorphisms]
    simp

lemma fromOfGlobalSections_morphismRestrict {r : A} {n : â„•} (hn : 0 < n) (hr : r âˆˆ ğ’œ n) :
    (fromOfGlobalSections ğ’œ f hf) âˆ£_ (basicOpen ğ’œ r) =
      (Scheme.isoOfEq _ (fromOfGlobalSections_preimage_basicOpen _ _ _ hn hr)).hom â‰«
        toBasicOpenOfGlobalSections ğ’œ f rfl hn hr := by
  rw [â† Iso.inv_comp_eq, â† cancel_mono (basicOpen ğ’œ r).Î¹]
  simp only [Scheme.isoOfEq_inv, Category.assoc, morphismRestrict_Î¹, Scheme.homOfLE_Î¹_assoc,
    fromOfGlobalSections]
  exact (openCoverOfMapIrrelevantEqTop ğ’œ f hf).Î¹_glueMorphisms _ _ âŸ¨_, _, hn, hrâŸ©

lemma fromOfGlobalSections_resLE {r : A} {n : â„•} (hn : 0 < n) (hr : r âˆˆ ğ’œ n) :
    (fromOfGlobalSections ğ’œ f hf).resLE _ _
      (fromOfGlobalSections_preimage_basicOpen _ _ _ hn hr).ge =
      toBasicOpenOfGlobalSections ğ’œ f rfl hn hr := by
  rw [â† (Iso.inv_comp_eq _).mpr (fromOfGlobalSections_morphismRestrict ğ’œ f hf hn hr),
    â† Scheme.Hom.resLE_eq_morphismRestrict]
  simp [Scheme.isoOfEq_inv]

@[reassoc]
lemma fromOfGlobalSections_toSpecZero
    (f : A â†’+* Î“(X, âŠ¤)) (hf : (HomogeneousIdeal.irrelevant ğ’œ).toIdeal.map f = âŠ¤) :
    fromOfGlobalSections ğ’œ f hf â‰« toSpecZero ğ’œ =
      X.toSpecÎ“ â‰« Spec.map (CommRingCat.ofHom (f.comp (algebraMap _ _))) := by
  refine (openCoverOfMapIrrelevantEqTop ğ’œ f hf).hom_ext _ _ fun x â†¦ ?_
  simp only [fromOfGlobalSections, toBasicOpenOfGlobalSections, CommRingCat.ofHom_comp,
    Category.assoc, Scheme.Cover.Î¹_glueMorphisms_assoc, basicOpenIsoSpec_inv_Î¹_assoc,
    awayÎ¹_toSpecZero, Iso.inv_comp_eq]
  simp only [openCoverOfMapIrrelevantEqTop, Scheme.openCoverOfIsOpenCover_X,
    Scheme.openCoverOfIsOpenCover_f, Scheme.isoOfEq_hom_Î¹_assoc, â† morphismRestrict_Î¹_assoc]
  congr 1
  simp only [basicOpenIsoSpecAway, â† CommRingCat.ofHom_comp, â† Spec.map_comp, â† Iso.eq_inv_comp,
    IsOpenImmersion.isoOfRangeEq_inv_fac_assoc, â† HomogeneousLocalization.algebraMap_eq]
  congr 2
  rw [RingHom.comp_assoc, â† IsScalarTower.algebraMap_eq, IsScalarTower.algebraMap_eq _ A,
    â† RingHom.comp_assoc, IsLocalization.map_comp, RingHom.comp_assoc]

end ofGlobalSection

end AlgebraicGeometry.Proj
