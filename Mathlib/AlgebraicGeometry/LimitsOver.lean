/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.AlgebraicGeometry.Morphisms.Basic
import Mathlib.CategoryTheory.MorphismProperty.Comma

/-!
# (Co)limits in over categories

We show that if `P` is a morphism property in `Scheme` that is local at the source, then
colimits in `P.Over âŠ¤ X` for `X : Scheme` of locally directed diagrams of open immersions
exist and agree with the colimit in `Scheme`.
-/

universe u

open CategoryTheory Limits

namespace AlgebraicGeometry

variable (X : Scheme.{u})

variable (P : MorphismProperty Scheme.{u})

section Over

variable {S : Scheme.{u}} {J : Type*} [Category J] (F : J â¥¤ Over S)
  [âˆ€ {i j} (f : i âŸ¶ j), IsOpenImmersion (F.map f).left]
  [(F â‹™ Over.forget S â‹™ Scheme.forget).IsLocallyDirected]
  [Quiver.IsThin J] [Small.{u} J]

noncomputable instance : HasColimit F :=
  have {i j} (f : i âŸ¶ j) : IsOpenImmersion ((F â‹™ Over.forget S).map f) :=
    inferInstanceAs <| IsOpenImmersion (F.map f).left
  have : ((F â‹™ Over.forget S) â‹™ Scheme.forget).IsLocallyDirected := â€¹_â€º
  hasColimit_of_created _ (Over.forget S)

end Over

section OverProp

instance {S : Scheme.{u}} {U X Y : P.Over âŠ¤ S} (f : U âŸ¶ X) (g : U âŸ¶ Y)
    [IsOpenImmersion f.left] [IsOpenImmersion g.left] (i : WalkingPair) :
    Mono ((span f g â‹™ MorphismProperty.Over.forget P âŠ¤ S â‹™ Over.forget S â‹™ Scheme.forget).map
      (WidePushoutShape.Hom.init i)) := by
  rw [mono_iff_injective]
  cases i
  Â· simpa using f.left.isOpenEmbedding.injective
  Â· simpa using g.left.isOpenEmbedding.injective

instance {S : Scheme.{u}} {U X Y : P.Over âŠ¤ S} (f : U âŸ¶ X) (g : U âŸ¶ Y)
    [IsOpenImmersion f.left] [IsOpenImmersion g.left]
    {i j : WalkingSpan} (t : i âŸ¶ j) :
      IsOpenImmersion ((span f g).map t).left := by
  obtain (a|(a|a)) := t
  Â· simp only [WidePushoutShape.hom_id, CategoryTheory.Functor.map_id]
    infer_instance
  Â· simpa
  Â· simpa

variable [IsZariskiLocalAtSource P] {S : Scheme.{u}} {J : Type*} [Category J] (F : J â¥¤ P.Over âŠ¤ S)
  [âˆ€ {i j} (f : i âŸ¶ j), IsOpenImmersion (F.map f).left]
  [(F â‹™ MorphismProperty.Over.forget P âŠ¤ S â‹™ Over.forget S â‹™ Scheme.forget).IsLocallyDirected]
  [Quiver.IsThin J] [Small.{u} J]

local instance :
    (((F â‹™ MorphismProperty.Over.forget P âŠ¤ S) â‹™ Over.forget S) â‹™
      Scheme.forget).IsLocallyDirected :=
  â€¹_â€º

local instance {i j} (f : i âŸ¶ j) :
    IsOpenImmersion <|
      ((F â‹™ MorphismProperty.Over.forget P âŠ¤ S) â‹™ Over.forget S).map f :=
  inferInstanceAs <| IsOpenImmersion (F.map f).left

noncomputable instance : CreatesColimit F (MorphismProperty.Over.forget P âŠ¤ S) := by
  have : HasColimit (F â‹™ MorphismProperty.Over.forget P âŠ¤ S) :=
    hasColimit_of_created _ (Over.forget S)
  refine createsColimitOfFullyFaithfulOfIso
      { toComma := colimit (F â‹™ MorphismProperty.Over.forget P âŠ¤ S)
        prop := ?_ } (Iso.refl _)
  let e : (colimit (F â‹™ MorphismProperty.Over.forget P âŠ¤ S)).left â‰…
      colimit ((F â‹™ MorphismProperty.Over.forget P âŠ¤ S) â‹™ Over.forget S) :=
    preservesColimitIso (Over.forget S) _
  let ð’° : (colimit (F â‹™ MorphismProperty.Over.forget P âŠ¤ S)).left.OpenCover :=
    (Scheme.IsLocallyDirected.openCover _).pushforwardIso e.inv
  rw [IsZariskiLocalAtSource.iff_of_openCover (P := P) ð’°]
  intro i
  simpa [ð’°, e] using (F.obj i).prop

instance : HasColimit F := hasColimit_of_created _ (MorphismProperty.Over.forget P âŠ¤ S)

instance : PreservesColimit F (MorphismProperty.Over.forget P âŠ¤ S) :=
  -- this is only `inferInstance` with the local instances above
  inferInstance

instance (j : J) : IsOpenImmersion (colimit.Î¹ F j).left := by
  rw [â† MorphismProperty.Over.forget_comp_forget_map]
  let e : (colimit F).left â‰… colimit (F â‹™ _) :=
    preservesColimitIso (MorphismProperty.Over.forget P âŠ¤ S â‹™ Over.forget S) F
  rw [â† MorphismProperty.cancel_right_of_respectsIso (P := @IsOpenImmersion) _ e.hom]
  simp only [e, CategoryTheory.Î¹_preservesColimitIso_hom]
  exact inferInstanceAs <| IsOpenImmersion
    (colimit.Î¹ ((F â‹™ MorphismProperty.Over.forget P âŠ¤ S) â‹™ Over.forget S) j)

instance {Î¹ : Type*} [Small.{u} Î¹] : HasCoproductsOfShape Î¹ (P.Over âŠ¤ S) where

instance : HasFiniteCoproducts (P.Over âŠ¤ S) where
  out := inferInstance

noncomputable instance (J : Type*) [Small.{u} J] :
    CreatesColimitsOfShape (Discrete J) (MorphismProperty.Over.forget P âŠ¤ S) where

end OverProp

end AlgebraicGeometry
