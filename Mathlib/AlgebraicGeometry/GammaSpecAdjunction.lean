/-
Copyright (c) 2021 Junyan Xu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Junyan Xu
-/
import Mathlib.AlgebraicGeometry.Restrict
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.CategoryTheory.Adjunction.Opposites
import Mathlib.CategoryTheory.Adjunction.Reflective

/-!
# Adjunction between `Î“` and `Spec`

We define the adjunction `Î“Spec.adjunction : Î“ âŠ£ Spec` by defining the unit (`toÎ“Spec`,
in multiple steps in this file) and counit (done in `Spec.lean`) and checking that they satisfy
the left and right triangle identities. The constructions and proofs make use of
maps and lemmas defined and proved in structure_sheaf.lean extensively.

Notice that since the adjunction is between contravariant functors, you get to choose
one of the two categories to have arrows reversed, and it is equally valid to present
the adjunction as `Spec âŠ£ Î“` (`Spec.to_LocallyRingedSpace.right_op âŠ£ Î“`), in which
case the unit and the counit would switch to each other.

## Main definition

* `AlgebraicGeometry.identityToÎ“Spec` : The natural transformation `ğŸ­ _ âŸ¶ Î“ â‹™ Spec`.
* `AlgebraicGeometry.Î“Spec.locallyRingedSpaceAdjunction` : The adjunction `Î“ âŠ£ Spec` from
  `CommRingáµ’áµ–` to `LocallyRingedSpace`.
* `AlgebraicGeometry.Î“Spec.adjunction` : The adjunction `Î“ âŠ£ Spec` from
  `CommRingáµ’áµ–` to `Scheme`.

-/

-- Explicit universe annotations were used in this file to improve performance https://github.com/leanprover-community/mathlib4/issues/12737


noncomputable section

universe u

open PrimeSpectrum

namespace AlgebraicGeometry

open Opposite

open CategoryTheory

open StructureSheaf

open Spec (structureSheaf)

open TopologicalSpace

open AlgebraicGeometry.LocallyRingedSpace

open TopCat.Presheaf

open TopCat.Presheaf.SheafCondition

namespace LocallyRingedSpace

variable (X : LocallyRingedSpace.{u})

/-- The canonical map from the underlying set to the prime spectrum of `Î“(X)`. -/
def toÎ“SpecFun : X â†’ PrimeSpectrum (Î“.obj (op X)) := fun x =>
  comap (X.presheaf.Î“germ x).hom (IsLocalRing.closedPoint (X.presheaf.stalk x))

theorem notMem_prime_iff_unit_in_stalk (r : Î“.obj (op X)) (x : X) :
    r âˆ‰ (X.toÎ“SpecFun x).asIdeal â†” IsUnit (X.presheaf.Î“germ x r) := by
  simp [toÎ“SpecFun, IsLocalRing.closedPoint]

@[deprecated (since := "2025-05-23")]
alias not_mem_prime_iff_unit_in_stalk := notMem_prime_iff_unit_in_stalk

/-- The preimage of a basic open in `Spec Î“(X)` under the unit is the basic
open in `X` defined by the same element (they are equal as sets). -/
theorem toÎ“Spec_preimage_basicOpen_eq (r : Î“.obj (op X)) :
    X.toÎ“SpecFun â»Â¹' basicOpen r = SetLike.coe (X.toRingedSpace.basicOpen r) := by
      ext
      dsimp
      simp only [Set.mem_preimage, SetLike.mem_coe]
      rw [X.toRingedSpace.mem_top_basicOpen]
      exact notMem_prime_iff_unit_in_stalk ..

/-- `toÎ“SpecFun` is continuous. -/
theorem toÎ“Spec_continuous : Continuous X.toÎ“SpecFun := by
  rw [isTopologicalBasis_basic_opens.continuous_iff]
  rintro _ âŸ¨r, rflâŸ©
  rw [X.toÎ“Spec_preimage_basicOpen_eq r]
  exact (X.toRingedSpace.basicOpen r).2

/-- The canonical (bundled) continuous map from the underlying topological
space of `X` to the prime spectrum of its global sections. -/
def toÎ“SpecBase : X.toTopCat âŸ¶ Spec.topObj (Î“.obj (op X)) :=
  TopCat.ofHom
  { toFun := X.toÎ“SpecFun
    continuous_toFun := X.toÎ“Spec_continuous }

variable (r : Î“.obj (op X))

/-- The preimage in `X` of a basic open in `Spec Î“(X)` (as an open set). -/
abbrev toÎ“SpecMapBasicOpen : Opens X :=
  (Opens.map X.toÎ“SpecBase).obj (basicOpen r)

/-- The preimage is the basic open in `X` defined by the same element `r`. -/
theorem toÎ“SpecMapBasicOpen_eq : X.toÎ“SpecMapBasicOpen r = X.toRingedSpace.basicOpen r :=
  Opens.ext (X.toÎ“Spec_preimage_basicOpen_eq r)

/-- The map from the global sections `Î“(X)` to the sections on the (preimage of) a basic open. -/
abbrev toToÎ“SpecMapBasicOpen :
    X.presheaf.obj (op âŠ¤) âŸ¶ X.presheaf.obj (op <| X.toÎ“SpecMapBasicOpen r) :=
  X.presheaf.map (X.toÎ“SpecMapBasicOpen r).leTop.op

/-- `r` is a unit as a section on the basic open defined by `r`. -/
theorem isUnit_res_toÎ“SpecMapBasicOpen : IsUnit (X.toToÎ“SpecMapBasicOpen r r) := by
  convert
    (X.presheaf.map <| (eqToHom <| X.toÎ“SpecMapBasicOpen_eq r).op).hom.isUnit_map
      (X.toRingedSpace.isUnit_res_basicOpen r)
  rw [â† CommRingCat.comp_apply, â† Functor.map_comp]
  congr

/-- Define the sheaf hom on individual basic opens for the unit. -/
def toÎ“SpecCApp :
    (structureSheaf <| Î“.obj <| op X).val.obj (op <| basicOpen r) âŸ¶
      X.presheaf.obj (op <| X.toÎ“SpecMapBasicOpen r) :=
  -- note: the explicit type annotations were not needed before
  -- https://github.com/leanprover-community/mathlib4/pull/19757
  CommRingCat.ofHom  <|
    IsLocalization.Away.lift
      (R := Î“.obj (op X))
      (S := (structureSheaf â†‘(Î“.obj (op X))).val.obj (op (basicOpen r)))
      r
      (isUnit_res_toÎ“SpecMapBasicOpen _ r)

/-- Characterization of the sheaf hom on basic opens,
direction â† (next lemma) is used at various places, but â†’ is not used in this file. -/
theorem toÎ“SpecCApp_iff
    (f :
      (structureSheaf <| Î“.obj <| op X).val.obj (op <| basicOpen r) âŸ¶
        X.presheaf.obj (op <| X.toÎ“SpecMapBasicOpen r)) :
    toOpen _ (basicOpen r) â‰« f = X.toToÎ“SpecMapBasicOpen r â†” f = X.toÎ“SpecCApp r := by
  have loc_inst := IsLocalization.to_basicOpen (Î“.obj (op X)) r
  refine ConcreteCategory.ext_iff.trans ?_
  rw [â† @IsLocalization.Away.lift_comp _ _ _ _ _ _ _ r loc_inst _
      (X.isUnit_res_toÎ“SpecMapBasicOpen r)]
  constructor
  Â· intro h
    ext : 1
    exact IsLocalization.ringHom_ext (Submonoid.powers r) h
  apply congr_arg

theorem toÎ“SpecCApp_spec : toOpen _ (basicOpen r) â‰« X.toÎ“SpecCApp r = X.toToÎ“SpecMapBasicOpen r :=
  (X.toÎ“SpecCApp_iff r _).2 rfl

/-- The sheaf hom on all basic opens, commuting with restrictions. -/
@[simps app]
def toÎ“SpecCBasicOpens :
    (inducedFunctor basicOpen).op â‹™ (structureSheaf (Î“.obj (op X))).1 âŸ¶
      (inducedFunctor basicOpen).op â‹™ ((TopCat.Sheaf.pushforward _ X.toÎ“SpecBase).obj X.ğ’ª).1 where
  app r := X.toÎ“SpecCApp r.unop
  naturality r s f := by
    apply (StructureSheaf.to_basicOpen_epi (Î“.obj (op X)) r.unop).1
    simp only [â† Category.assoc]
    rw [X.toÎ“SpecCApp_spec r.unop]
    convert X.toÎ“SpecCApp_spec s.unop
    symm
    apply X.presheaf.map_comp

/-- The canonical morphism of sheafed spaces from `X` to the spectrum of its global sections. -/
@[simps]
def toÎ“SpecSheafedSpace : X.toSheafedSpace âŸ¶ Spec.toSheafedSpace.obj (op (Î“.obj (op X))) where
  base := X.toÎ“SpecBase
  c :=
    TopCat.Sheaf.restrictHomEquivHom (structureSheaf (Î“.obj (op X))).1 _ isBasis_basic_opens
      X.toÎ“SpecCBasicOpens

theorem toÎ“SpecSheafedSpace_app_eq :
    X.toÎ“SpecSheafedSpace.c.app (op (basicOpen r)) = X.toÎ“SpecCApp r := by
  apply TopCat.Sheaf.extend_hom_app _ _ _

@[reassoc] theorem toÎ“SpecSheafedSpace_app_spec (r : Î“.obj (op X)) :
    toOpen (Î“.obj (op X)) (basicOpen r) â‰« X.toÎ“SpecSheafedSpace.c.app (op (basicOpen r)) =
      X.toToÎ“SpecMapBasicOpen r :=
  (X.toÎ“SpecSheafedSpace_app_eq r).symm â–¸ X.toÎ“SpecCApp_spec r

/-- The map on stalks induced by the unit commutes with maps from `Î“(X)` to
stalks (in `Spec Î“(X)` and in `X`). -/
theorem toStalk_stalkMap_toÎ“Spec (x : X) :
    toStalk _ _ â‰« X.toÎ“SpecSheafedSpace.stalkMap x = X.presheaf.Î“germ x := by
  rw [PresheafedSpace.Hom.stalkMap,
    â† toOpen_germ _ (basicOpen (1 : Î“.obj (op X))) _ (by rw [basicOpen_one]; trivial),
    â† Category.assoc, Category.assoc (toOpen _ _), stalkFunctor_map_germ, â† Category.assoc,
    X.toÎ“SpecSheafedSpace_app_eq, X.toÎ“SpecCApp_spec, Î“germ]
  erw [â† stalkPushforward_germ _ _ X.presheaf âŠ¤]
  congr 1
  exact (X.toÎ“SpecBase _* X.presheaf).germ_res le_top.hom _ _

/-- The canonical morphism from `X` to the spectrum of its global sections. -/
@[simps! base]
def toÎ“Spec : X âŸ¶ Spec.locallyRingedSpaceObj (Î“.obj (op X)) where
  __ := X.toÎ“SpecSheafedSpace
  prop := by
    intro x
    let p : PrimeSpectrum (Î“.obj (op X)) := X.toÎ“SpecFun x
    constructor
    -- show stalk map is local hom â†“
    let S := (structureSheaf _).presheaf.stalk p
    rintro (t : S) ht
    obtain âŸ¨âŸ¨r, sâŸ©, heâŸ© := IsLocalization.surj p.asIdeal.primeCompl t
    dsimp at he
    set t' := _
    change t * t' = _ at he
    apply isUnit_of_mul_isUnit_left (y := t')
    rw [he]
    refine IsLocalization.map_units S (âŸ¨r, ?_âŸ© : p.asIdeal.primeCompl)
    apply (notMem_prime_iff_unit_in_stalk _ _ _).mpr
    rw [â† toStalk_stalkMap_toÎ“Spec, CommRingCat.comp_apply]
    erw [â† he]
    rw [RingHom.map_mul]
    exact ht.mul <| (IsLocalization.map_units (R := Î“.obj (op X)) S s).map _

/-- On a locally ringed space `X`, the preimage of the zero locus of the prime spectrum
of `Î“(X, âŠ¤)` under `toÎ“Spec` agrees with the associated zero locus on `X`. -/
lemma toÎ“Spec_preimage_zeroLocus_eq {X : LocallyRingedSpace.{u}}
    (s : Set (X.presheaf.obj (op âŠ¤))) :
    X.toÎ“Spec.base â»Â¹' PrimeSpectrum.zeroLocus s = X.toRingedSpace.zeroLocus s := by
  simp only [RingedSpace.zeroLocus]
  have (i : LocallyRingedSpace.Î“.obj (op X)) (_ : i âˆˆ s) :
      (SetLike.coe (X.toRingedSpace.basicOpen i))á¶œ =
        X.toÎ“Spec.base â»Â¹' ((PrimeSpectrum.basicOpen i).carrier)á¶œ := by
    symm
    rw [Set.preimage_compl, Opens.carrier_eq_coe]
    erw [X.toÎ“Spec_preimage_basicOpen_eq i]
  erw [Set.iInterâ‚‚_congr this]
  simp_rw [â† Set.preimage_iInterâ‚‚, Opens.carrier_eq_coe, PrimeSpectrum.basicOpen_eq_zeroLocus_compl,
    compl_compl]
  rw [â† PrimeSpectrum.zeroLocus_iUnionâ‚‚]
  simp

theorem comp_ring_hom_ext {X : LocallyRingedSpace.{u}} {R : CommRingCat.{u}} {f : R âŸ¶ Î“.obj (op X)}
    {Î² : X âŸ¶ Spec.locallyRingedSpaceObj R}
    (w : X.toÎ“Spec.base â‰« (Spec.locallyRingedSpaceMap f).base = Î².base)
    (h :
      âˆ€ r : R,
        f â‰« X.presheaf.map (homOfLE le_top : (Opens.map Î².base).obj (basicOpen r) âŸ¶ _).op =
          toOpen R (basicOpen r) â‰« Î².c.app (op (basicOpen r))) :
    X.toÎ“Spec â‰« Spec.locallyRingedSpaceMap f = Î² := by
  ext1
  refine Spec.basicOpen_hom_ext w ?_
  intro r U
  rw [LocallyRingedSpace.comp_c_app]
  erw [toOpen_comp_comap_assoc]
  rw [Category.assoc]
  erw [toÎ“SpecSheafedSpace_app_spec, â† X.presheaf.map_comp]
  exact h r

/-- `toSpecÎ“ _` is an isomorphism so these are mutually two-sided inverses. -/
theorem Î“_Spec_left_triangle : toSpecÎ“ (Î“.obj (op X)) â‰« X.toÎ“Spec.c.app (op âŠ¤) = ğŸ™ _ := by
  unfold toSpecÎ“
  rw [â† toOpen_res _ (basicOpen (1 : Î“.obj (op X))) âŠ¤ (eqToHom basicOpen_one.symm),
    Category.assoc, NatTrans.naturality, â† Category.assoc]
  erw [X.toÎ“SpecSheafedSpace_app_spec 1, â† Functor.map_comp]
  convert eqToHom_map X.presheaf _; rfl

end LocallyRingedSpace

/-- The unit as a natural transformation. -/
def identityToÎ“Spec : ğŸ­ LocallyRingedSpace.{u} âŸ¶ Î“.rightOp â‹™ Spec.toLocallyRingedSpace where
  app := LocallyRingedSpace.toÎ“Spec
  naturality X Y f := by
    symm
    apply LocallyRingedSpace.comp_ring_hom_ext
    Â· ext1 x
      dsimp
      change PrimeSpectrum.comap (f.c.app (op âŠ¤)).hom (X.toÎ“SpecFun x) = Y.toÎ“SpecFun (f.base x)
      dsimp [toÎ“SpecFun]
      rw [â† IsLocalRing.comap_closedPoint (f.stalkMap x).hom, â†
        PrimeSpectrum.comap_comp_apply, â† PrimeSpectrum.comap_comp_apply,
        â† CommRingCat.hom_comp, â† CommRingCat.hom_comp]
      congr 3
      exact (PresheafedSpace.stalkMap_germ f.1 âŠ¤ x trivial).symm
    Â· intro r
      rw [LocallyRingedSpace.comp_c_app, â† Category.assoc]
      erw [Y.toÎ“SpecSheafedSpace_app_spec, f.c.naturality]
      rfl

namespace Î“Spec

theorem left_triangle (X : LocallyRingedSpace) :
    SpecÎ“Identity.inv.app (Î“.obj (op X)) â‰« (identityToÎ“Spec.app X).c.app (op âŠ¤) = ğŸ™ _ :=
  X.Î“_Spec_left_triangle

/-- `SpecÎ“Identity` is iso so these are mutually two-sided inverses. -/
theorem right_triangle (R : CommRingCat) :
    identityToÎ“Spec.app (Spec.toLocallyRingedSpace.obj <| op R) â‰«
        Spec.toLocallyRingedSpace.map (SpecÎ“Identity.inv.app R).op =
      ğŸ™ _ := by
  apply LocallyRingedSpace.comp_ring_hom_ext
  Â· ext (p : PrimeSpectrum R)
    dsimp
    refine PrimeSpectrum.ext (Ideal.ext fun x => ?_)
    rw [â† IsLocalization.AtPrime.to_map_mem_maximal_iff ((structureSheaf R).presheaf.stalk p)
        p.asIdeal x]
    rfl
  Â· intro r; apply toOpen_res

/-- The adjunction `Î“ âŠ£ Spec` from `CommRingáµ’áµ–` to `LocallyRingedSpace`. -/
@[simps]
def locallyRingedSpaceAdjunction : Î“.rightOp âŠ£ Spec.toLocallyRingedSpace.{u} where
  unit := identityToÎ“Spec
  counit := (NatIso.op SpecÎ“Identity).inv
  left_triangle_components X := by
    simp only [Functor.id_obj, Functor.rightOp_obj, Î“_obj, Functor.comp_obj,
      Spec.toLocallyRingedSpace_obj, Spec.locallyRingedSpaceObj_toSheafedSpace,
      Spec.sheafedSpaceObj_carrier, Spec.sheafedSpaceObj_presheaf, Functor.rightOp_map, Î“_map,
      Quiver.Hom.unop_op, NatIso.op_inv, NatTrans.op_app, SpecÎ“Identity_inv_app]
    exact congr_arg Quiver.Hom.op (left_triangle X)
  right_triangle_components R := by
    simp only [Spec.toLocallyRingedSpace_obj, Functor.id_obj, Functor.comp_obj, Functor.rightOp_obj,
      Î“_obj, Spec.locallyRingedSpaceObj_toSheafedSpace, Spec.sheafedSpaceObj_carrier,
      Spec.sheafedSpaceObj_presheaf, NatIso.op_inv, NatTrans.op_app, op_unop, SpecÎ“Identity_inv_app,
      Spec.toLocallyRingedSpace_map, Quiver.Hom.unop_op]
    exact right_triangle R.unop

/-- `@[simp]`-normal form of `locallyRingedSpaceAdjunction_counit_app`. -/
@[simp]
lemma toSpecÎ“_unop (R : CommRingCatáµ’áµ–) :
    AlgebraicGeometry.toSpecÎ“ (Opposite.unop R) = toOpen R.unop âŠ¤ := rfl

/-- `@[simp]`-normal form of `locallyRingedSpaceAdjunction_counit_app'`. -/
@[simp]
lemma toSpecÎ“_of (R : Type u) [CommRing R] :
    AlgebraicGeometry.toSpecÎ“ (CommRingCat.of R) = toOpen R âŠ¤ := rfl

lemma locallyRingedSpaceAdjunction_counit_app (R : CommRingCatáµ’áµ–) :
    locallyRingedSpaceAdjunction.counit.app R =
      (toOpen R.unop âŠ¤).op := rfl

lemma locallyRingedSpaceAdjunction_counit_app' (R : Type u) [CommRing R] :
    locallyRingedSpaceAdjunction.counit.app (op <| CommRingCat.of R) =
      (toOpen R âŠ¤).op := rfl

lemma locallyRingedSpaceAdjunction_homEquiv_apply
    {X : LocallyRingedSpace} {R : CommRingCatáµ’áµ–}
    (f : Î“.rightOp.obj X âŸ¶ R) :
    locallyRingedSpaceAdjunction.homEquiv X R f =
      identityToÎ“Spec.app X â‰« Spec.locallyRingedSpaceMap f.unop := rfl

lemma locallyRingedSpaceAdjunction_homEquiv_apply'
    {X : LocallyRingedSpace} {R : Type u} [CommRing R]
    (f : CommRingCat.of R âŸ¶ Î“.obj <| op X) :
    locallyRingedSpaceAdjunction.homEquiv X (op <| CommRingCat.of R) (op f) =
      identityToÎ“Spec.app X â‰« Spec.locallyRingedSpaceMap f := rfl

lemma toOpen_comp_locallyRingedSpaceAdjunction_homEquiv_app
    {X : LocallyRingedSpace} {R : Type u} [CommRing R]
    (f : Î“.rightOp.obj X âŸ¶ op (CommRingCat.of R)) (U) :
    StructureSheaf.toOpen R U.unop â‰«
      (locallyRingedSpaceAdjunction.homEquiv X (op <| CommRingCat.of R) f).c.app U =
    f.unop â‰« X.presheaf.map (homOfLE le_top).op := by
  rw [â† StructureSheaf.toOpen_res _ _ _ (homOfLE le_top), Category.assoc,
    NatTrans.naturality _ (homOfLE (le_top (a := U.unop))).op,
    show (toOpen R âŠ¤) = (toOpen R âŠ¤).op.unop from rfl,
    â† locallyRingedSpaceAdjunction_counit_app']
  simp_rw [â† Î“_map_op]
  rw [â† Î“.rightOp_map_unop, â† Category.assoc, â† unop_comp, â† Adjunction.homEquiv_counit,
    Equiv.symm_apply_apply]
  rfl

/-- The adjunction `Î“ âŠ£ Spec` from `CommRingáµ’áµ–` to `Scheme`. -/
def adjunction : Scheme.Î“.rightOp âŠ£ Scheme.Spec.{u} where
  unit :=
  { app := fun X â†¦ âŸ¨locallyRingedSpaceAdjunction.{u}.unit.app X.toLocallyRingedSpaceâŸ©
    naturality := fun _ _ f â†¦
      Scheme.Hom.ext' (locallyRingedSpaceAdjunction.{u}.unit.naturality f.toLRSHom) }
  counit := (NatIso.op Scheme.SpecÎ“Identity.{u}).inv
  left_triangle_components Y :=
    locallyRingedSpaceAdjunction.left_triangle_components Y.toLocallyRingedSpace
  right_triangle_components R :=
    Scheme.Hom.ext' <| locallyRingedSpaceAdjunction.right_triangle_components R

theorem adjunction_homEquiv_apply {X : Scheme} {R : CommRingCatáµ’áµ–}
    (f : (op <| Scheme.Î“.obj <| op X) âŸ¶ R) :
    Î“Spec.adjunction.homEquiv X R f = âŸ¨locallyRingedSpaceAdjunction.homEquiv X.1 R fâŸ© := rfl

theorem adjunction_homEquiv_symm_apply {X : Scheme} {R : CommRingCatáµ’áµ–}
    (f : X âŸ¶ Scheme.Spec.obj R) :
    (Î“Spec.adjunction.homEquiv X R).symm f =
      (locallyRingedSpaceAdjunction.homEquiv X.1 R).symm f.toLRSHom := rfl

theorem adjunction_counit_app' {R : CommRingCatáµ’áµ–} :
    Î“Spec.adjunction.counit.app R = locallyRingedSpaceAdjunction.counit.app R := rfl

@[simp]
theorem adjunction_counit_app {R : CommRingCatáµ’áµ–} :
    Î“Spec.adjunction.counit.app R = (Scheme.Î“SpecIso (unop R)).inv.op := rfl

/-- The canonical map `X âŸ¶ Spec Î“(X, âŠ¤)`. This is the unit of the `Î“-Spec` adjunction. -/
def _root_.AlgebraicGeometry.Scheme.toSpecÎ“ (X : Scheme.{u}) : X âŸ¶ Spec Î“(X, âŠ¤) :=
  Î“Spec.adjunction.unit.app X

@[simp]
theorem adjunction_unit_app {X : Scheme} :
    Î“Spec.adjunction.unit.app X = X.toSpecÎ“ := rfl

instance isIso_locallyRingedSpaceAdjunction_counit :
    IsIso.{u + 1, u + 1} locallyRingedSpaceAdjunction.counit :=
  (NatIso.op SpecÎ“Identity).isIso_inv

instance isIso_adjunction_counit : IsIso Î“Spec.adjunction.counit := by
  apply (config := { allowSynthFailures := true }) NatIso.isIso_of_isIso_app
  intro R
  rw [adjunction_counit_app]
  infer_instance

end Î“Spec

theorem Scheme.toSpecÎ“_base (X : Scheme.{u}) (x) :
    (Scheme.toSpecÎ“ X).base x =
      (Spec.map (X.presheaf.germ âŠ¤ x trivial)).base (IsLocalRing.closedPoint _) := rfl

@[reassoc]
theorem Scheme.toSpecÎ“_naturality {X Y : Scheme.{u}} (f : X âŸ¶ Y) :
    f â‰« Y.toSpecÎ“ = X.toSpecÎ“ â‰« Spec.map (f.appTop) :=
  Î“Spec.adjunction.unit.naturality f

@[simp]
theorem Scheme.toSpecÎ“_appTop (X : Scheme.{u}) :
    X.toSpecÎ“.appTop = (Scheme.Î“SpecIso Î“(X, âŠ¤)).hom := by
  have := Î“Spec.adjunction.left_triangle_components X
  dsimp at this
  rw [â† IsIso.eq_comp_inv] at this
  simp only [Category.id_comp] at this
  rw [â† Quiver.Hom.op_inj.eq_iff, this, â† op_inv, IsIso.Iso.inv_inv]

@[simp]
theorem SpecMap_Î“SpecIso_hom (R : CommRingCat.{u}) :
    Spec.map ((Scheme.Î“SpecIso R).hom) = (Spec R).toSpecÎ“ := by
  have := Î“Spec.adjunction.right_triangle_components (op R)
  dsimp at this
  rwa [â† IsIso.eq_comp_inv, Category.id_comp, â† Spec.map_inv, IsIso.Iso.inv_inv, eq_comm] at this

lemma Scheme.toSpecÎ“_preimage_basicOpen (X : Scheme.{u}) (r : Î“(X, âŠ¤)) :
    X.toSpecÎ“ â»Â¹áµ (PrimeSpectrum.basicOpen r) = X.basicOpen r := by
  rw [â† basicOpen_eq_of_affine, Scheme.preimage_basicOpen, â† Scheme.Hom.appTop]
  congr
  rw [Scheme.toSpecÎ“_appTop]
  exact Iso.inv_hom_id_apply (C := CommRingCat) _ _

-- Warning: this LHS of this lemma breaks the structure-sheaf abstraction.
@[reassoc (attr := simp)]
theorem toOpen_toSpecÎ“_app {X : Scheme.{u}} (U) :
    StructureSheaf.toOpen _ _ â‰« X.toSpecÎ“.app U =
      X.presheaf.map (homOfLE (by exact le_top)).op := by
  rw [â† StructureSheaf.toOpen_res _ _ _ (homOfLE le_top), Category.assoc,
    NatTrans.naturality _ (homOfLE (le_top (a := U))).op]
  change (Î“Spec.adjunction.counit.app (Scheme.Î“.rightOp.obj X)).unop â‰«
    (Scheme.Î“.rightOp.map (Î“Spec.adjunction.unit.app X)).unop â‰« _ = _
  rw [â† Category.assoc, â† unop_comp, Î“Spec.adjunction.left_triangle_components]
  dsimp
  exact Category.id_comp _

lemma Î“SpecIso_inv_Î“Spec_adjunction_homEquiv {X : Scheme.{u}} {B : CommRingCat} (Ï† : B âŸ¶ Î“(X, âŠ¤)) :
    (Scheme.Î“SpecIso B).inv â‰« ((Î“Spec.adjunction.homEquiv X (op B)) Ï†.op).appTop = Ï† := by
  simp only [Adjunction.homEquiv_apply, Scheme.Spec_map, Opens.map_top, Scheme.comp_app]
  simp

lemma Î“Spec_adjunction_homEquiv_eq {X : Scheme.{u}} {B : CommRingCat} (Ï† : B âŸ¶ Î“(X, âŠ¤)) :
    ((Î“Spec.adjunction.homEquiv X (op B)) Ï†.op).appTop = (Scheme.Î“SpecIso B).hom â‰« Ï† := by
  rw [â† Iso.inv_comp_eq, Î“SpecIso_inv_Î“Spec_adjunction_homEquiv]

theorem Î“SpecIso_obj_hom {X : Scheme.{u}} (U : X.Opens) :
    (Scheme.Î“SpecIso Î“(X, U)).hom = (Spec.map U.topIso.inv).appTop â‰«
      U.toScheme.toSpecÎ“.appTop â‰« U.topIso.hom := by simp

/-! Immediate consequences of the adjunction. -/



/-- The functor `Spec.toLocallyRingedSpace : CommRingCatáµ’áµ– â¥¤ LocallyRingedSpace`
is fully faithful. -/
def Spec.fullyFaithfulToLocallyRingedSpace : Spec.toLocallyRingedSpace.FullyFaithful :=
  Î“Spec.locallyRingedSpaceAdjunction.fullyFaithfulROfIsIsoCounit

/-- Spec is a full functor. -/
instance : Spec.toLocallyRingedSpace.Full :=
  Spec.fullyFaithfulToLocallyRingedSpace.full

/-- Spec is a faithful functor. -/
instance : Spec.toLocallyRingedSpace.Faithful :=
  Spec.fullyFaithfulToLocallyRingedSpace.faithful

/-- The functor `Spec : CommRingCatáµ’áµ– â¥¤ Scheme` is fully faithful. -/
def Spec.fullyFaithful : Scheme.Spec.FullyFaithful :=
  Î“Spec.adjunction.fullyFaithfulROfIsIsoCounit

/-- Spec is a full functor. -/
instance Spec.full : Scheme.Spec.Full :=
  Spec.fullyFaithful.full

/-- Spec is a faithful functor. -/
instance Spec.faithful : Scheme.Spec.Faithful :=
  Spec.fullyFaithful.faithful

section

variable {R S : CommRingCat.{u}} {Ï† Ïˆ : R âŸ¶ S} (f : Spec S âŸ¶ Spec R)

lemma Spec.map_inj : Spec.map Ï† = Spec.map Ïˆ â†” Ï† = Ïˆ := by
  rw [iff_comm, â† Quiver.Hom.op_inj.eq_iff, â† Scheme.Spec.map_injective.eq_iff]
  rfl

lemma Spec.map_injective {R S : CommRingCat} : Function.Injective (Spec.map : (R âŸ¶ S) â†’ _) :=
  fun _ _ â†¦ Spec.map_inj.mp

@[simp]
lemma Spec.map_eq_id {R : CommRingCat} {Ï• : R âŸ¶ R} : Spec.map Ï• = ğŸ™ (Spec R) â†” Ï• = ğŸ™ R := by
  simp [â† map_inj]

/-- The preimage under Spec. -/
def Spec.preimage : R âŸ¶ S := (Scheme.Spec.preimage f).unop

@[simp] lemma Spec.map_preimage : Spec.map (Spec.preimage f) = f := Scheme.Spec.map_preimage f

@[simp] lemma Spec.map_preimage_unop (f : Spec R âŸ¶ Spec S) :
    Spec.map (Spec.fullyFaithful.preimage f).unop = f := Spec.fullyFaithful.map_preimage _

variable (Ï†) in
@[simp] lemma Spec.preimage_map : Spec.preimage (Spec.map Ï†) = Ï† :=
  Spec.map_injective (Spec.map_preimage (Spec.map Ï†))

/-- Useful for replacing `f` by `Spec.map Ï†` everywhere in proofs. -/
lemma Spec.map_surjective {R S : CommRingCat} :
    Function.Surjective (Spec.map : (R âŸ¶ S) â†’ _) := by
  intro f
  use Spec.preimage f
  simp

/-- Spec is fully faithful -/
@[simps]
def Spec.homEquiv {R S : CommRingCat} : (Spec S âŸ¶ Spec R) â‰ƒ (R âŸ¶ S) where
  toFun := Spec.preimage
  invFun := Spec.map
  left_inv := Spec.map_preimage
  right_inv := Spec.preimage_map

@[simp]
lemma Spec.preimage_id {R : CommRingCat} : Spec.preimage (ğŸ™ (Spec R)) = ğŸ™ R :=
  Spec.map_injective (by simp)

@[simp, reassoc]
lemma Spec.preimage_comp {R S T : CommRingCat} (f : Spec R âŸ¶ Spec S) (g : Spec S âŸ¶ Spec T) :
    Spec.preimage (f â‰« g) = Spec.preimage g â‰« Spec.preimage f :=
  Spec.map_injective (by simp)

end

instance : Reflective Spec.toLocallyRingedSpace where
  L := Î“.rightOp
  adj := Î“Spec.locallyRingedSpaceAdjunction

instance Spec.reflective : Reflective Scheme.Spec where
  L := Scheme.Î“.rightOp
  adj := Î“Spec.adjunction

instance : LocallyRingedSpace.Î“.IsRightAdjoint :=
  Î“Spec.locallyRingedSpaceAdjunction.rightOp.isRightAdjoint

instance : Scheme.Î“.IsRightAdjoint := Î“Spec.adjunction.rightOp.isRightAdjoint

end AlgebraicGeometry
