/-
Copyright (c) 2021 Junyan Xu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Junyan Xu
-/
import Mathlib.AlgebraicGeometry.Restrict
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.CategoryTheory.Adjunction.Reflective

#align_import algebraic_geometry.Gamma_Spec_adjunction from "leanprover-community/mathlib"@"d39590fc8728fbf6743249802486f8c91ffe07bc"

/-!
# Adjunction between `Î“` and `Spec`

We define the adjunction `Î“Spec.adjunction : Î“ âŠ£ Spec` by defining the unit (`toÎ“Spec`,
in multiple steps in this file) and counit (done in `Spec.lean`) and checking that they satisfy
the left and right triangle identities. The constructions and proofs make use of
maps and lemmas defined and proved in structure_sheaf.lean extensively.

Notice that since the adjunction is between contravariant functors, you get to choose
one of the two categories to have arrows reversed, and it is equally valid to present
the adjunction as `Spec âŠ£ Î“` (`Spec.to_LocallyRingedSpace.right_op âŠ£ Î“`), in which
case the unit and the counit would switch to each other.

## Main definition

* `AlgebraicGeometry.identityToÎ“Spec` : The natural transformation `ğŸ­ _ âŸ¶ Î“ â‹™ Spec`.
* `AlgebraicGeometry.Î“Spec.locallyRingedSpaceAdjunction` : The adjunction `Î“ âŠ£ Spec` from
  `CommRingáµ’áµ–` to `LocallyRingedSpace`.
* `AlgebraicGeometry.Î“Spec.adjunction` : The adjunction `Î“ âŠ£ Spec` from
  `CommRingáµ’áµ–` to `Scheme`.

-/

-- Explicit universe annotations were used in this file to improve perfomance #12737

set_option linter.uppercaseLean3 false

noncomputable section

universe u

open PrimeSpectrum

namespace AlgebraicGeometry

open Opposite

open CategoryTheory

open StructureSheaf

open Spec (structureSheaf)

open TopologicalSpace

open AlgebraicGeometry.LocallyRingedSpace

open TopCat.Presheaf

open TopCat.Presheaf.SheafCondition

namespace LocallyRingedSpace

variable (X : LocallyRingedSpace.{u})

/-- The map from the global sections to a stalk. -/
def Î“ToStalk (x : X) : Î“.obj (op X) âŸ¶ X.presheaf.stalk x :=
  X.presheaf.germ (âŸ¨x, trivialâŸ© : (âŠ¤ : Opens X))
#align algebraic_geometry.LocallyRingedSpace.Î“_to_stalk AlgebraicGeometry.LocallyRingedSpace.Î“ToStalk

/-- The canonical map from the underlying set to the prime spectrum of `Î“(X)`. -/
def toÎ“SpecFun : X â†’ PrimeSpectrum (Î“.obj (op X)) := fun x =>
  comap (X.Î“ToStalk x) (LocalRing.closedPoint (X.presheaf.stalk x))
#align algebraic_geometry.LocallyRingedSpace.to_Î“_Spec_fun AlgebraicGeometry.LocallyRingedSpace.toÎ“SpecFun

theorem not_mem_prime_iff_unit_in_stalk (r : Î“.obj (op X)) (x : X) :
    r âˆ‰ (X.toÎ“SpecFun x).asIdeal â†” IsUnit (X.Î“ToStalk x r) := by
  erw [LocalRing.mem_maximalIdeal, Classical.not_not]
#align algebraic_geometry.LocallyRingedSpace.not_mem_prime_iff_unit_in_stalk AlgebraicGeometry.LocallyRingedSpace.not_mem_prime_iff_unit_in_stalk

/-- The preimage of a basic open in `Spec Î“(X)` under the unit is the basic
open in `X` defined by the same element (they are equal as sets). -/
theorem toÎ“Spec_preim_basicOpen_eq (r : Î“.obj (op X)) :
    X.toÎ“SpecFun â»Â¹' (basicOpen r).1 = (X.toRingedSpace.basicOpen r).1 := by
      ext
      erw [X.toRingedSpace.mem_top_basicOpen]; apply not_mem_prime_iff_unit_in_stalk
#align algebraic_geometry.LocallyRingedSpace.to_Î“_Spec_preim_basic_open_eq AlgebraicGeometry.LocallyRingedSpace.toÎ“Spec_preim_basicOpen_eq

/-- `toÎ“SpecFun` is continuous. -/
theorem toÎ“Spec_continuous : Continuous X.toÎ“SpecFun := by
  rw [isTopologicalBasis_basic_opens.continuous_iff]
  rintro _ âŸ¨r, rflâŸ©
  erw [X.toÎ“Spec_preim_basicOpen_eq r]
  exact (X.toRingedSpace.basicOpen r).2
#align algebraic_geometry.LocallyRingedSpace.to_Î“_Spec_continuous AlgebraicGeometry.LocallyRingedSpace.toÎ“Spec_continuous

/-- The canonical (bundled) continuous map from the underlying topological
space of `X` to the prime spectrum of its global sections. -/
@[simps]
def toÎ“SpecBase : X.toTopCat âŸ¶ Spec.topObj (Î“.obj (op X)) where
  toFun := X.toÎ“SpecFun
  continuous_toFun := X.toÎ“Spec_continuous
#align algebraic_geometry.LocallyRingedSpace.to_Î“_Spec_base AlgebraicGeometry.LocallyRingedSpace.toÎ“SpecBase

-- These lemmas have always been bad (#7657), but lean4#2644 made `simp` start noticing
attribute [nolint simpNF] AlgebraicGeometry.LocallyRingedSpace.toÎ“SpecBase_apply

variable (r : Î“.obj (op X))

/-- The preimage in `X` of a basic open in `Spec Î“(X)` (as an open set). -/
abbrev toÎ“SpecMapBasicOpen : Opens X :=
  (Opens.map X.toÎ“SpecBase).obj (basicOpen r)
#align algebraic_geometry.LocallyRingedSpace.to_Î“_Spec_map_basic_open AlgebraicGeometry.LocallyRingedSpace.toÎ“SpecMapBasicOpen

/-- The preimage is the basic open in `X` defined by the same element `r`. -/
theorem toÎ“SpecMapBasicOpen_eq : X.toÎ“SpecMapBasicOpen r = X.toRingedSpace.basicOpen r :=
  Opens.ext (X.toÎ“Spec_preim_basicOpen_eq r)
#align algebraic_geometry.LocallyRingedSpace.to_Î“_Spec_map_basic_open_eq AlgebraicGeometry.LocallyRingedSpace.toÎ“SpecMapBasicOpen_eq

/-- The map from the global sections `Î“(X)` to the sections on the (preimage of) a basic open. -/
abbrev toToÎ“SpecMapBasicOpen :
    X.presheaf.obj (op âŠ¤) âŸ¶ X.presheaf.obj (op <| X.toÎ“SpecMapBasicOpen r) :=
  X.presheaf.map (X.toÎ“SpecMapBasicOpen r).leTop.op
#align algebraic_geometry.LocallyRingedSpace.to_to_Î“_Spec_map_basic_open AlgebraicGeometry.LocallyRingedSpace.toToÎ“SpecMapBasicOpen

/-- `r` is a unit as a section on the basic open defined by `r`. -/
theorem isUnit_res_toÎ“SpecMapBasicOpen : IsUnit (X.toToÎ“SpecMapBasicOpen r r) := by
  convert
    (X.presheaf.map <| (eqToHom <| X.toÎ“SpecMapBasicOpen_eq r).op).isUnit_map
      (X.toRingedSpace.isUnit_res_basicOpen r)
  -- Porting note: `rw [comp_apply]` to `erw [comp_apply]`
  erw [â† comp_apply, â† Functor.map_comp]
  congr
#align algebraic_geometry.LocallyRingedSpace.is_unit_res_to_Î“_Spec_map_basic_open AlgebraicGeometry.LocallyRingedSpace.isUnit_res_toÎ“SpecMapBasicOpen

/-- Define the sheaf hom on individual basic opens for the unit. -/
def toÎ“SpecCApp :
    (structureSheaf <| Î“.obj <| op X).val.obj (op <| basicOpen r) âŸ¶
      X.presheaf.obj (op <| X.toÎ“SpecMapBasicOpen r) :=
  IsLocalization.Away.lift r (isUnit_res_toÎ“SpecMapBasicOpen _ r)
#align algebraic_geometry.LocallyRingedSpace.to_Î“_Spec_c_app AlgebraicGeometry.LocallyRingedSpace.toÎ“SpecCApp

/-- Characterization of the sheaf hom on basic opens,
    direction â† (next lemma) is used at various places, but â†’ is not used in this file. -/
theorem toÎ“SpecCApp_iff
    (f :
      (structureSheaf <| Î“.obj <| op X).val.obj (op <| basicOpen r) âŸ¶
        X.presheaf.obj (op <| X.toÎ“SpecMapBasicOpen r)) :
    toOpen _ (basicOpen r) â‰« f = X.toToÎ“SpecMapBasicOpen r â†” f = X.toÎ“SpecCApp r := by
  -- Porting Note: Type class problem got stuck in `IsLocalization.Away.AwayMap.lift_comp`
  -- created instance manually. This replaces the `pick_goal` tactics
  have loc_inst := IsLocalization.to_basicOpen (Î“.obj (op X)) r
  rw [â† @IsLocalization.Away.AwayMap.lift_comp _ _ _ _ _ _ _ r loc_inst _
      (X.isUnit_res_toÎ“SpecMapBasicOpen r)]
  --pick_goal 5; exact is_localization.to_basic_open _ r
  constructor
  Â· intro h
    exact IsLocalization.ringHom_ext (Submonoid.powers r) h
  apply congr_arg
#align algebraic_geometry.LocallyRingedSpace.to_Î“_Spec_c_app_iff AlgebraicGeometry.LocallyRingedSpace.toÎ“SpecCApp_iff

theorem toÎ“SpecCApp_spec : toOpen _ (basicOpen r) â‰« X.toÎ“SpecCApp r = X.toToÎ“SpecMapBasicOpen r :=
  (X.toÎ“SpecCApp_iff r _).2 rfl
#align algebraic_geometry.LocallyRingedSpace.to_Î“_Spec_c_app_spec AlgebraicGeometry.LocallyRingedSpace.toÎ“SpecCApp_spec

/-- The sheaf hom on all basic opens, commuting with restrictions. -/
@[simps app]
def toÎ“SpecCBasicOpens :
    (inducedFunctor basicOpen).op â‹™ (structureSheaf (Î“.obj (op X))).1 âŸ¶
      (inducedFunctor basicOpen).op â‹™ ((TopCat.Sheaf.pushforward _ X.toÎ“SpecBase).obj X.ğ’ª).1 where
  app r := X.toÎ“SpecCApp r.unop
  naturality r s f := by
    apply (StructureSheaf.to_basicOpen_epi (Î“.obj (op X)) r.unop).1
    simp only [â† Category.assoc]
    erw [X.toÎ“SpecCApp_spec r.unop]
    convert X.toÎ“SpecCApp_spec s.unop
    symm
    apply X.presheaf.map_comp
#align algebraic_geometry.LocallyRingedSpace.to_Î“_Spec_c_basic_opens AlgebraicGeometry.LocallyRingedSpace.toÎ“SpecCBasicOpens

/-- The canonical morphism of sheafed spaces from `X` to the spectrum of its global sections. -/
@[simps]
def toÎ“SpecSheafedSpace : X.toSheafedSpace âŸ¶ Spec.toSheafedSpace.obj (op (Î“.obj (op X))) where
  base := X.toÎ“SpecBase
  c :=
    TopCat.Sheaf.restrictHomEquivHom (structureSheaf (Î“.obj (op X))).1 _ isBasis_basic_opens
      X.toÎ“SpecCBasicOpens
#align algebraic_geometry.LocallyRingedSpace.to_Î“_Spec_SheafedSpace AlgebraicGeometry.LocallyRingedSpace.toÎ“SpecSheafedSpace

-- Porting Note: Now need much more hand holding: all variables explicit, and need to tidy up
-- significantly, was `TopCat.Sheaf.extend_hom_app _ _ _ _`
theorem toÎ“SpecSheafedSpace_app_eq :
    X.toÎ“SpecSheafedSpace.c.app (op (basicOpen r)) = X.toÎ“SpecCApp r := by
  have := TopCat.Sheaf.extend_hom_app (Spec.toSheafedSpace.obj (op (Î“.obj (op X)))).presheaf
    ((TopCat.Sheaf.pushforward _ X.toÎ“SpecBase).obj X.ğ’ª)
    isBasis_basic_opens X.toÎ“SpecCBasicOpens r
  dsimp at this
  rw [â† this]
  dsimp

#align algebraic_geometry.LocallyRingedSpace.to_Î“_Spec_SheafedSpace_app_eq AlgebraicGeometry.LocallyRingedSpace.toÎ“SpecSheafedSpace_app_eq

-- Porting note: need a helper lemma `toÎ“SpecSheafedSpace_app_spec_assoc` to help compile
-- `toStalk_stalkMap_to_Î“_Spec`
@[reassoc] theorem toÎ“SpecSheafedSpace_app_spec (r : Î“.obj (op X)) :
    toOpen (Î“.obj (op X)) (basicOpen r) â‰« X.toÎ“SpecSheafedSpace.c.app (op (basicOpen r)) =
      X.toToÎ“SpecMapBasicOpen r :=
  (X.toÎ“SpecSheafedSpace_app_eq r).symm â–¸ X.toÎ“SpecCApp_spec r
#align algebraic_geometry.LocallyRingedSpace.to_Î“_Spec_SheafedSpace_app_spec AlgebraicGeometry.LocallyRingedSpace.toÎ“SpecSheafedSpace_app_spec

/-- The map on stalks induced by the unit commutes with maps from `Î“(X)` to
    stalks (in `Spec Î“(X)` and in `X`). -/
theorem toStalk_stalkMap_toÎ“Spec (x : X) :
    toStalk _ _ â‰« PresheafedSpace.stalkMap X.toÎ“SpecSheafedSpace x = X.Î“ToStalk x := by
  rw [PresheafedSpace.stalkMap]
  erw [â† toOpen_germ _ (basicOpen (1 : Î“.obj (op X)))
      âŸ¨X.toÎ“SpecFun x, by rw [basicOpen_one]; trivialâŸ©]
  rw [â† Category.assoc, Category.assoc (toOpen _ _)]
  erw [stalkFunctor_map_germ]
  rw [â† Category.assoc, toÎ“SpecSheafedSpace_app_spec]
  unfold Î“ToStalk
  rw [â† stalkPushforward_germ _ X.toÎ“SpecBase X.presheaf âŠ¤]
  congr 1
  change (X.toÎ“SpecBase _* X.presheaf).map le_top.hom.op â‰« _ = _
  apply germ_res
#align algebraic_geometry.LocallyRingedSpace.to_stalk_stalk_map_to_Î“_Spec AlgebraicGeometry.LocallyRingedSpace.toStalk_stalkMap_toÎ“Spec

/-- The canonical morphism from `X` to the spectrum of its global sections. -/
@[simps! val_base]
def toÎ“Spec : X âŸ¶ Spec.locallyRingedSpaceObj (Î“.obj (op X)) where
  val := X.toÎ“SpecSheafedSpace
  prop := by
    intro x
    let p : PrimeSpectrum (Î“.obj (op X)) := X.toÎ“SpecFun x
    constructor
    -- show stalk map is local hom â†“
    let S := (structureSheaf _).presheaf.stalk p
    rintro (t : S) ht
    obtain âŸ¨âŸ¨r, sâŸ©, heâŸ© := IsLocalization.surj p.asIdeal.primeCompl t
    dsimp at he
    set t' := _
    change t * t' = _ at he
    apply isUnit_of_mul_isUnit_left (y := t')
    rw [he]
    refine IsLocalization.map_units S (âŸ¨r, ?_âŸ© : p.asIdeal.primeCompl)
    apply (not_mem_prime_iff_unit_in_stalk _ _ _).mpr
    rw [â† toStalk_stalkMap_toÎ“Spec]
    erw [comp_apply, â† he]
    rw [RingHom.map_mul]
    -- Porting note: `IsLocalization.map_units` and the goal needs to be simplified before Lean
    -- realize it is useful
    have := IsLocalization.map_units (R := Î“.obj (op X)) S s
    dsimp at this âŠ¢
    exact ht.mul <| this.map _
#align algebraic_geometry.LocallyRingedSpace.to_Î“_Spec AlgebraicGeometry.LocallyRingedSpace.toÎ“Spec

theorem comp_ring_hom_ext {X : LocallyRingedSpace.{u}} {R : CommRingCat.{u}} {f : R âŸ¶ Î“.obj (op X)}
    {Î² : X âŸ¶ Spec.locallyRingedSpaceObj R}
    (w : X.toÎ“Spec.1.base â‰« (Spec.locallyRingedSpaceMap f).1.base = Î².1.base)
    (h :
      âˆ€ r : R,
        f â‰« X.presheaf.map (homOfLE le_top : (Opens.map Î².1.base).obj (basicOpen r) âŸ¶ _).op =
          toOpen R (basicOpen r) â‰« Î².1.c.app (op (basicOpen r))) :
    X.toÎ“Spec â‰« Spec.locallyRingedSpaceMap f = Î² := by
  ext1
  -- Porting note: was `apply Spec.basicOpen_hom_ext`
  refine Spec.basicOpen_hom_ext w ?_
  intro r U
  rw [LocallyRingedSpace.comp_val_c_app]
  erw [toOpen_comp_comap_assoc]
  rw [Category.assoc]
  erw [toÎ“SpecSheafedSpace_app_spec, â† X.presheaf.map_comp]
  exact h r
#align algebraic_geometry.LocallyRingedSpace.comp_ring_hom_ext AlgebraicGeometry.LocallyRingedSpace.comp_ring_hom_ext

/-- `toSpecÎ“ _` is an isomorphism so these are mutually two-sided inverses. -/
theorem Î“_Spec_left_triangle : toSpecÎ“ (Î“.obj (op X)) â‰« X.toÎ“Spec.1.c.app (op âŠ¤) = ğŸ™ _ := by
  unfold toSpecÎ“
  rw [â† toOpen_res _ (basicOpen (1 : Î“.obj (op X))) âŠ¤ (eqToHom basicOpen_one.symm)]
  erw [Category.assoc]
  rw [NatTrans.naturality, â† Category.assoc]
  erw [X.toÎ“SpecSheafedSpace_app_spec 1, â† Functor.map_comp]
  convert eqToHom_map X.presheaf _; rfl
#align algebraic_geometry.LocallyRingedSpace.Î“_Spec_left_triangle AlgebraicGeometry.LocallyRingedSpace.Î“_Spec_left_triangle

end LocallyRingedSpace

/-- The unit as a natural transformation. -/
def identityToÎ“Spec : ğŸ­ LocallyRingedSpace.{u} âŸ¶ Î“.rightOp â‹™ Spec.toLocallyRingedSpace where
  app := LocallyRingedSpace.toÎ“Spec
  naturality X Y f := by
    symm
    apply LocallyRingedSpace.comp_ring_hom_ext
    Â· ext1 x
      dsimp only [Spec.topMap, LocallyRingedSpace.toÎ“SpecFun]
      -- Porting note: Had to add the next four lines
      rw [comp_apply]
      dsimp [toÎ“SpecBase]
      -- The next six lines were `rw [ContinuousMap.coe_mk, ContinuousMap.coe_mk]` before
      -- leanprover/lean4#2644
      have : (ContinuousMap.mk (toÎ“SpecFun Y) (toÎ“Spec_continuous _)) (f.val.base x)
        = toÎ“SpecFun Y (f.val.base x) := by rw [ContinuousMap.coe_mk]
      erw [this]
      have : (ContinuousMap.mk (toÎ“SpecFun X) (toÎ“Spec_continuous _)) x
        = toÎ“SpecFun X x := by rw [ContinuousMap.coe_mk]
      erw [this]
      dsimp [toÎ“SpecFun]
      -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
      erw [â† LocalRing.comap_closedPoint (PresheafedSpace.stalkMap f.val x), â†
        PrimeSpectrum.comap_comp_apply, â† PrimeSpectrum.comap_comp_apply]
      congr 2
      exact (PresheafedSpace.stalkMap_germ f.1 âŠ¤ âŸ¨x, trivialâŸ©).symm
    Â· intro r
      rw [LocallyRingedSpace.comp_val_c_app, â† Category.assoc]
      erw [Y.toÎ“SpecSheafedSpace_app_spec, f.1.c.naturality]
      rfl
#align algebraic_geometry.identity_to_Î“_Spec AlgebraicGeometry.identityToÎ“Spec

namespace Î“Spec

set_option backward.isDefEq.lazyWhnfCore false in -- See https://github.com/leanprover-community/mathlib4/issues/12534
theorem left_triangle (X : LocallyRingedSpace) :
    SpecÎ“Identity.inv.app (Î“.obj (op X)) â‰« (identityToÎ“Spec.app X).val.c.app (op âŠ¤) = ğŸ™ _ :=
  X.Î“_Spec_left_triangle
#align algebraic_geometry.Î“_Spec.left_triangle AlgebraicGeometry.Î“Spec.left_triangle

/-- `SpecÎ“Identity` is iso so these are mutually two-sided inverses. -/
theorem right_triangle (R : CommRingCat) :
    identityToÎ“Spec.app (Spec.toLocallyRingedSpace.obj <| op R) â‰«
        Spec.toLocallyRingedSpace.map (SpecÎ“Identity.inv.app R).op =
      ğŸ™ _ := by
  apply LocallyRingedSpace.comp_ring_hom_ext
  Â· ext (p : PrimeSpectrum R)
    dsimp
    ext x
    erw [â† IsLocalization.AtPrime.to_map_mem_maximal_iff ((structureSheaf R).presheaf.stalk p)
        p.asIdeal x]
    rfl
  Â· intro r; apply toOpen_res
#align algebraic_geometry.Î“_Spec.right_triangle AlgebraicGeometry.Î“Spec.right_triangle

/-- The adjunction `Î“ âŠ£ Spec` from `CommRingáµ’áµ–` to `LocallyRingedSpace`. -/
-- Porting note: `simps` cause a time out, so `Unit` and `counit` will be added manually
def locallyRingedSpaceAdjunction : Î“.rightOp âŠ£ Spec.toLocallyRingedSpace.{u} :=
  Adjunction.mkOfUnitCounit
    { unit := identityToÎ“Spec
      counit := (NatIso.op SpecÎ“Identity).inv
      left_triangle := by
        ext X; erw [Category.id_comp]
        exact congr_arg Quiver.Hom.op (left_triangle X)
      right_triangle := by
        ext R : 2
        -- Porting note: a little bit hand holding
        change identityToÎ“Spec.app _ â‰« ğŸ™ _ â‰« Spec.toLocallyRingedSpace.map _ =
          ğŸ™ _
        simp_rw [Category.id_comp, show (NatIso.op SpecÎ“Identity).inv.app R =
          (SpecÎ“Identity.inv.app R.unop).op from rfl]
        exact right_triangle R.unop
        }
#align algebraic_geometry.Î“_Spec.LocallyRingedSpace_adjunction AlgebraicGeometry.Î“Spec.locallyRingedSpaceAdjunction

lemma locallyRingedSpaceAdjunction_unit :
    locallyRingedSpaceAdjunction.unit = identityToÎ“Spec := rfl
#align algebraic_geometry.Î“_Spec.LocallyRingedSpace_adjunction_unit AlgebraicGeometry.Î“Spec.locallyRingedSpaceAdjunction_unit

lemma locallyRingedSpaceAdjunction_counit :
    locallyRingedSpaceAdjunction.counit = (NatIso.op SpecÎ“Identity.{u}).inv := rfl
#align algebraic_geometry.Î“_Spec.LocallyRingedSpace_adjunction_counit AlgebraicGeometry.Î“Spec.locallyRingedSpaceAdjunction_counit

-- Porting Note: Commented
--attribute [local semireducible] Spec.toLocallyRingedSpace

/-- The adjunction `Î“ âŠ£ Spec` from `CommRingáµ’áµ–` to `Scheme`. -/
def adjunction : Scheme.Î“.rightOp âŠ£ Scheme.Spec :=
  locallyRingedSpaceAdjunction.restrictFullyFaithful
    Scheme.fullyFaithfulForgetToLocallyRingedSpace (Functor.FullyFaithful.id _)
    (NatIso.ofComponents (fun X => Iso.refl _))
    (NatIso.ofComponents (fun X => Iso.refl _))
#align algebraic_geometry.Î“_Spec.adjunction AlgebraicGeometry.Î“Spec.adjunction

theorem adjunction_homEquiv_apply {X : Scheme} {R : CommRingCatáµ’áµ–}
    (f : (op <| Scheme.Î“.obj <| op X) âŸ¶ R) :
    Î“Spec.adjunction.homEquiv X R f = locallyRingedSpaceAdjunction.homEquiv X.1 R f := by
  dsimp only [adjunction]
  rw [Adjunction.restrictFullyFaithful_homEquiv_apply, Adjunction.homEquiv_unit]
  simp
#align algebraic_geometry.Î“_Spec.adjunction_hom_equiv_apply AlgebraicGeometry.Î“Spec.adjunction_homEquiv_apply

theorem adjunction_homEquiv (X : Scheme) (R : CommRingCatáµ’áµ–) :
    Î“Spec.adjunction.homEquiv X R = locallyRingedSpaceAdjunction.homEquiv X.1 R :=
  Equiv.ext fun f => adjunction_homEquiv_apply f
#align algebraic_geometry.Î“_Spec.adjunction_hom_equiv AlgebraicGeometry.Î“Spec.adjunction_homEquiv

theorem adjunction_homEquiv_symm_apply {X : Scheme} {R : CommRingCatáµ’áµ–}
    (f : X âŸ¶ Scheme.Spec.obj R) :
    (Î“Spec.adjunction.homEquiv X R).symm f = (locallyRingedSpaceAdjunction.homEquiv X.1 R).symm f :=
  by rw [adjunction_homEquiv]; rfl
#align algebraic_geometry.Î“_Spec.adjunction_hom_equiv_symm_apply AlgebraicGeometry.Î“Spec.adjunction_homEquiv_symm_apply

set_option backward.isDefEq.lazyWhnfCore false in -- See https://github.com/leanprover-community/mathlib4/issues/12534
@[simp]
theorem adjunction_counit_app {R : CommRingCatáµ’áµ–} :
    Î“Spec.adjunction.counit.app R = locallyRingedSpaceAdjunction.counit.app R := by
  rw [â† Adjunction.homEquiv_symm_id, â† Adjunction.homEquiv_symm_id,
    adjunction_homEquiv_symm_apply]
  rfl
#align algebraic_geometry.Î“_Spec.adjunction_counit_app AlgebraicGeometry.Î“Spec.adjunction_counit_app

@[simp]
theorem adjunction_unit_app {X : Scheme} :
    Î“Spec.adjunction.unit.app X = locallyRingedSpaceAdjunction.unit.app X.1 := by
  rw [â† Adjunction.homEquiv_id, â† Adjunction.homEquiv_id, adjunction_homEquiv_apply]; rfl
#align algebraic_geometry.Î“_Spec.adjunction_unit_app AlgebraicGeometry.Î“Spec.adjunction_unit_app

-- Porting Note: Commented
-- attribute [local semireducible] locallyRingedSpaceAdjunction Î“Spec.adjunction

instance isIso_locallyRingedSpaceAdjunction_counit :
    IsIso.{u + 1, u + 1} locallyRingedSpaceAdjunction.counit :=
  (NatIso.op SpecÎ“Identity).isIso_inv
#align algebraic_geometry.Î“_Spec.is_iso_LocallyRingedSpace_adjunction_counit AlgebraicGeometry.Î“Spec.isIso_locallyRingedSpaceAdjunction_counit

instance isIso_adjunction_counit : IsIso Î“Spec.adjunction.counit := by
  apply (config := { allowSynthFailures := true }) NatIso.isIso_of_isIso_app
  intro R
  rw [adjunction_counit_app]
  infer_instance
#align algebraic_geometry.Î“_Spec.is_iso_adjunction_counit AlgebraicGeometry.Î“Spec.isIso_adjunction_counit

theorem adjunction_unit_app_app_top (X : Scheme.{u}) :
    (Î“Spec.adjunction.unit.app X).1.c.app (op âŠ¤) =
    SpecÎ“Identity.hom.app (X.presheaf.obj (op âŠ¤)) := by
  have := congr_app Î“Spec.adjunction.left_triangle X
  dsimp at this
  -- Porting note: Slightly changed some rewrites.
  -- Originally:
  --  rw [â† is_iso.eq_comp_inv] at this
  --  simp only [Î“_Spec.LocallyRingedSpace_adjunction_counit, nat_trans.op_app, category.id_comp,
  --    Î“_Spec.adjunction_counit_app] at this
  --  rw [â† op_inv, nat_iso.inv_inv_app, quiver.hom.op_inj.eq_iff] at this
  rw [â† IsIso.eq_comp_inv] at this
  simp only [adjunction_counit_app, locallyRingedSpaceAdjunction_counit, NatIso.op_inv,
    NatTrans.op_app, unop_op, Functor.id_obj, Functor.comp_obj, Functor.rightOp_obj,
    Spec.toLocallyRingedSpace_obj, Î“_obj, Spec.locallyRingedSpaceObj_toSheafedSpace,
    Spec.sheafedSpaceObj_carrier, Spec.sheafedSpaceObj_presheaf,
    SpecÎ“Identity_inv_app, Category.id_comp] at this
  rw [â† op_inv, Quiver.Hom.op_inj.eq_iff] at this
  -- Note: changed from `rw` to `simp_rw` to improve performance
  simp_rw [SpecÎ“Identity_hom_app]
  exact this
#align algebraic_geometry.Î“_Spec.adjunction_unit_app_app_top AlgebraicGeometry.Î“Spec.adjunction_unit_app_app_top

end Î“Spec

@[reassoc]
theorem SpecÎ“Identity_naturality {R S : CommRingCat.{u}} (f : R âŸ¶ S) :
    (Scheme.Spec.map f.op).1.c.app (op âŠ¤) â‰« SpecÎ“Identity.hom.app _ =
      SpecÎ“Identity.hom.app _ â‰« f := SpecÎ“Identity.hom.naturality f

theorem SpecÎ“Identity_hom_app_presheaf_obj {X : Scheme.{u}} (U : Opens X) :
    SpecÎ“Identity.hom.app (X.presheaf.obj (op U)) =
      Scheme.Î“.map (Scheme.Spec.map (X.presheaf.map (eqToHom U.openEmbedding_obj_top).op).op).op â‰«
      (Î“Spec.adjunction.unit.app (X âˆ£_áµ¤ U)).val.c.app (op âŠ¤) â‰«
      X.presheaf.map (eqToHom U.openEmbedding_obj_top.symm).op := by
  rw [Î“Spec.adjunction_unit_app_app_top]
  dsimp [-SpecÎ“Identity_hom_app]
  rw [SpecÎ“Identity_naturality_assoc, â† Functor.map_comp, â† op_comp, eqToHom_trans, eqToHom_refl,
    op_id, CategoryTheory.Functor.map_id, Category.comp_id]

/-! Immediate consequences of the adjunction. -/


/-- Spec preserves limits. -/
instance : Limits.PreservesLimits Spec.toLocallyRingedSpace :=
  Î“Spec.locallyRingedSpaceAdjunction.rightAdjointPreservesLimits

instance Spec.preservesLimits : Limits.PreservesLimits Scheme.Spec :=
  Î“Spec.adjunction.rightAdjointPreservesLimits
#align algebraic_geometry.Spec.preserves_limits AlgebraicGeometry.Spec.preservesLimits

/-- The functor `Spec.toLocallyRingedSpace : CommRingCatáµ’áµ– â¥¤ LocallyRingedSpace`
is fully faithful.-/
def Spec.fullyFaithfulToLocallyRingedSpace : Spec.toLocallyRingedSpace.FullyFaithful :=
  Î“Spec.locallyRingedSpaceAdjunction.fullyFaithfulROfIsIsoCounit

/-- Spec is a full functor. -/
instance : Spec.toLocallyRingedSpace.Full :=
  Spec.fullyFaithfulToLocallyRingedSpace.full

/-- Spec is a faithful functor. -/
instance : Spec.toLocallyRingedSpace.Faithful :=
  Spec.fullyFaithfulToLocallyRingedSpace.faithful

/-- The functor `Spec : CommRingCatáµ’áµ– â¥¤ Scheme` is fully faithful.-/
def Spec.fullyFaithful : Scheme.Spec.FullyFaithful :=
  Î“Spec.adjunction.fullyFaithfulROfIsIsoCounit

/-- Spec is a full functor. -/
instance Spec.full : Scheme.Spec.Full  :=
  Spec.fullyFaithful.full
#align algebraic_geometry.Spec.full AlgebraicGeometry.Spec.full

/-- Spec is a faithful functor. -/
instance Spec.faithful : Scheme.Spec.Faithful :=
  Spec.fullyFaithful.faithful
#align algebraic_geometry.Spec.faithful AlgebraicGeometry.Spec.faithful

instance : Spec.toLocallyRingedSpace.IsRightAdjoint  :=
  (Î“Spec.locallyRingedSpaceAdjunction).isRightAdjoint

instance : Scheme.Spec.IsRightAdjoint  :=
  (Î“Spec.adjunction).isRightAdjoint

instance : Reflective Spec.toLocallyRingedSpace where
  adj := Î“Spec.locallyRingedSpaceAdjunction

instance Spec.reflective : Reflective Scheme.Spec where
  adj := Î“Spec.adjunction
#align algebraic_geometry.Spec.reflective AlgebraicGeometry.Spec.reflective

end AlgebraicGeometry
