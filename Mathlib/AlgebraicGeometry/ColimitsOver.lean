/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
module

public import Mathlib.AlgebraicGeometry.RelativeGluing
public import Mathlib.CategoryTheory.MorphismProperty.OverAdjunction

/-!

# Colimits in `P.Over âŠ¤ S`

Let `D : J â¥¤ P.Over âŠ¤ S` be a diagram and `ğ’°` a locally directed open cover of `S`
(e.g., the cover of all affine opens of `S`). Suppose the restrictions
of `D` to `Dáµ¢ : J â¥¤ P.Over âŠ¤ (ğ’°.X i)` have a colimit for every `i`, then we show
that also `D` has a colimit under suitable assumptions.

This can be used to reduce existence of certain colimits in `P.Over âŠ¤ S` to the case where
`S` is affine.

-/

@[expose] public section

universe u

open CategoryTheory Limits MorphismProperty

namespace AlgebraicGeometry.Scheme.Cover

variable {P : MorphismProperty Scheme.{u}} [P.IsStableUnderBaseChange] [P.IsMultiplicative]
  {S : Scheme.{u}} {J : Type*} [Category* J] (D : J â¥¤ P.Over âŠ¤ S)
  (ğ’° : S.OpenCover) [Category* ğ’°.Iâ‚€] [ğ’°.LocallyDirected]

/-- The data required for gluing the colimits of the `Dáµ¢ : J â¥¤ P.Over âŠ¤ (ğ’°.X i)`. -/
structure ColimitGluingData (D : J â¥¤ P.Over âŠ¤ S) (ğ’° : S.OpenCover)
    [Category* ğ’°.Iâ‚€] [ğ’°.LocallyDirected] where
  /-- The cocones on the `Dáµ¢`. -/
  cocone (i : ğ’°.Iâ‚€) : Cocone (D â‹™ MorphismProperty.Over.pullback P âŠ¤ (ğ’°.f i))
  /-- The cocones on the `Dáµ¢` are colimiting. -/
  isColimit (i : ğ’°.Iâ‚€) : IsColimit (cocone i)
  prop_trans {i j : ğ’°.Iâ‚€} (hij : i âŸ¶ j) : P (ğ’°.trans hij)

namespace ColimitGluingData

variable {D} {ğ’°} (d : ColimitGluingData D ğ’°)

/-- (Implementation) Auxiliary natural transformation for construction of
`AlgebraicGeometry.Scheme.Cover.ColimitGluingData.functor`. -/
@[simps!]
noncomputable
def trans {i j : ğ’°.Iâ‚€} (hij : i âŸ¶ j) :
    D â‹™ Over.pullback P âŠ¤ (ğ’°.f i) â‹™ Over.map _ (d.prop_trans hij) âŸ¶
      D â‹™ Over.pullback P âŠ¤ (ğ’°.f j) :=
  D.whiskerLeft <| Over.pullbackMapHomPullback (P := P) (Q := âŠ¤) _ _ trivial _ _

variable [âˆ€ {i j} (hij : i âŸ¶ j),
  PreservesColimitsOfShape J (Over.map âŠ¤ (d.prop_trans hij))]

/-- (Implementation) Cocone for transition map for construction of
`AlgebraicGeometry.Scheme.Cover.ColimitGluingData.functor`. -/
@[simps! pt Î¹_app]
noncomputable
def transitionCocone {i j : ğ’°.Iâ‚€} (hij : i âŸ¶ j) :
    Cocone (D â‹™ Over.pullback P âŠ¤ (ğ’°.f i) â‹™ Over.map _ (d.prop_trans hij)) :=
  (Cocones.precompose (d.trans hij)).obj (d.cocone j)

/-- (Implementation) Transition map for construction of
`AlgebraicGeometry.Scheme.Cover.ColimitGluingData.functor`. -/
noncomputable
def transitionMap {i j : ğ’°.Iâ‚€} (hij : i âŸ¶ j) :
    (Over.map âŠ¤ (d.prop_trans hij)).obj (d.cocone i).pt âŸ¶ (d.cocone j).pt :=
  (isColimitOfPreserves (Over.map âŠ¤ (d.prop_trans hij)) (d.isColimit i)).desc
    (d.transitionCocone hij)

@[reassoc]
lemma cocone_Î¹_transitionMap {i j : ğ’°.Iâ‚€} (hij : i âŸ¶ j) (a : J) :
    (Over.map âŠ¤ (d.prop_trans hij)).map ((d.cocone i).Î¹.app a) â‰«
      d.transitionMap hij = (d.trans hij).app a â‰« (d.cocone j).Î¹.app a := by
  simp [transitionMap, â† Functor.mapCocone_Î¹_app, IsColimit.fac, transitionCocone]

@[simp]
lemma transitionMap_id (i : ğ’°.Iâ‚€) :
    d.transitionMap (ğŸ™ i) = ((Over.mapId _ _ _).hom.app <| (d.cocone i).pt) := by
  apply (isColimitOfPreserves (Over.map âŠ¤ (d.prop_trans <| ğŸ™ i)) (d.isColimit i)).hom_ext
  intro
  rw [transitionMap, IsColimit.fac]
  ext
  simp [transitionCocone]

@[simp]
lemma transitionMap_comp {i j k : ğ’°.Iâ‚€} (hij : i âŸ¶ j) (hjk : j âŸ¶ k) :
    d.transitionMap (hij â‰« hjk) =
      (Over.mapComp âŠ¤ (d.prop_trans hij) (d.prop_trans hjk) (ğ’°.trans (hij â‰« hjk))).hom.app _ â‰«
      (Over.map âŠ¤ (d.prop_trans hjk)).map (d.transitionMap hij) â‰«
        d.transitionMap hjk := by
  apply (isColimitOfPreserves (Over.map âŠ¤ _) (d.isColimit i)).hom_ext
  intro a
  simp only [transitionMap, IsColimit.fac]
  simp only [Functor.comp_obj, transitionCocone_pt, Functor.mapCocone_pt, Functor.const_obj_obj,
    Functor.mapCocone_Î¹_app, NatTrans.naturality_assoc, Functor.comp_map]
  rw [â† Functor.map_comp_assoc, â† Functor.mapCocone_Î¹_app, IsColimit.fac]
  simp only [transitionCocone, Cocones.precompose_obj_Î¹, NatTrans.comp_app,
    Functor.map_comp, Category.assoc]
  rw [â† Functor.mapCocone_Î¹_app, IsColimit.fac]
  ext
  simp [pullback.map_comp_assoc]

/-- (Implementation): Underlying functor of associated relative gluing datum. -/
@[simps]
noncomputable def functor : ğ’°.Iâ‚€ â¥¤ Scheme where
  obj i := (d.cocone i).pt.left
  map {i j} hij := (d.transitionMap hij).left

variable [âˆ€ {i j} (hij : i âŸ¶ j), PreservesColimitsOfShape J (Over.pullback P âŠ¤ (ğ’°.trans hij))]

lemma isPullback {i j : ğ’°.Iâ‚€} (hij : i âŸ¶ j) :
    IsPullback (d.transitionMap hij).left (d.cocone i).pt.hom
      (d.cocone j).pt.hom (ğ’°.trans hij) := by
  let iso1 : (d.cocone i).pt â‰… _ :=
    (d.isColimit i).coconePointsIsoOfNatIso
      (isColimitOfPreserves (Over.pullback P âŠ¤ (ğ’°.trans hij)) (d.isColimit j)) <|
      D.isoWhiskerLeft (Over.pullbackComp _ _ _) â‰ªâ‰« (Functor.associator _ _ _).symm
  have heq : (Over.map _ (d.prop_trans hij)).map iso1.hom â‰«
        (Over.mapPullbackAdj _ _ _ (d.prop_trans hij) trivial).counit.app _ =
      d.transitionMap hij := by
    apply (isColimitOfPreserves (Over.map _ (d.prop_trans hij)) (d.isColimit i)).hom_ext
    intro a
    dsimp
    simp only [IsColimit.coconePointsIsoOfNatIso_hom, Iso.trans_hom, Functor.isoWhiskerLeft_hom,
      iso1, â† Functor.map_comp_assoc, IsColimit.Î¹_map, Functor.mapCocone_pt,
      Functor.mapCocone_Î¹_app, Functor.map_comp, Category.assoc, Adjunction.counit_naturality]
    rw [transitionMap, â† Functor.mapCocone_Î¹_app, IsColimit.fac]
    ext
    simp only [Comma.comp_hom, CategoryTheory.Comma.comp_left, â† Category.assoc]
    congr 1
    apply pullback.hom_ext <;> simp
  let iso2 : (d.cocone i).pt.left â‰… pullback (d.cocone j).pt.hom (ğ’°.trans hij) :=
    (MorphismProperty.Over.forget _ _ _ â‹™ Over.forget _).mapIso iso1
  refine .of_iso (IsPullback.of_hasPullback _ _) iso2.symm (.refl _) (.refl _) (.refl _) ?_ ?_
      (by simp) (by simp)
  Â· simpa [â† cancel_epi iso2.hom] using congr($(heq).left)
  Â· exact (Over.w iso1.inv).symm

/-- The relative gluing datum associated to the family of the `colim Dáµ¢`. -/
@[simps natTrans_app, simps -isSimp functor]
noncomputable
def relativeGluingData : ğ’°.RelativeGluingData where
  functor := d.functor
  natTrans.app i := (d.cocone i).pt.hom
  equifibered {i j} hij := d.isPullback hij

variable [Quiver.IsThin ğ’°.Iâ‚€] [Small.{u} ğ’°.Iâ‚€] [IsZariskiLocalAtTarget P]

/-- The result of gluing the `colim Dáµ¢`. -/
noncomputable def glued : P.Over âŠ¤ S :=
  Over.mk _ d.relativeGluingData.toBase <| by
    rw [IsZariskiLocalAtTarget.iff_of_openCover (P := P) ğ’°]
    intro i
    rw [Scheme.Cover.pullbackHom,
      â† (d.relativeGluingData.isPullback_natTrans_Î¹_toBase i).flip.isoPullback_inv_snd,
      P.cancel_left_of_respectsIso]
    exact (d.cocone i).pt.prop

/-- `colim Dáµ¢` is the pullback of the glued object over `S` along the inclusion `ğ’°áµ¢ âŸ¶ S`. -/
noncomputable
def pullbackGluedIso (i : ğ’°.Iâ‚€) :
    (MorphismProperty.Over.pullback P âŠ¤ (ğ’°.f i)).obj d.glued â‰… (d.cocone i).pt :=
  Over.isoMk (d.relativeGluingData.isPullback_natTrans_Î¹_toBase i).flip.isoPullback.symm

@[reassoc (attr := simp)]
lemma pullbackGluedIso_inv_fst (i : ğ’°.Iâ‚€) : (d.pullbackGluedIso i).inv.left â‰« pullback.fst _ _ =
    colimit.Î¹ d.relativeGluingData.functor i := by
  simp [pullbackGluedIso, glued]

@[reassoc (attr := simp)]
lemma pullbackGluedIso_inv_snd (i : ğ’°.Iâ‚€) :
    (d.pullbackGluedIso i).inv.left â‰« pullback.snd _ _ = (d.cocone i).pt.hom := by
  simp [pullbackGluedIso, glued]

/-- The cocone glued from the `colim Dáµ¢`. -/
@[simps pt]
noncomputable def gluedCocone : Cocone D := by
  letI ğ’± (a : J) : (D.obj a).left.OpenCover := ğ’°.pullbackâ‚ (D.obj a).hom
  refine { pt := d.glued, Î¹.app a := ?_, Î¹.naturality {a b} f := ?_ }
  Â· refine âŸ¨(ğ’± a).glueMorphismsOverOfLocallyDirected ?_ ?_ ?_, trivial, trivialâŸ©
    Â· intro i
      exact ((d.cocone i).Î¹.app a).left â‰« colimit.Î¹ d.relativeGluingData.functor i
    Â· intro i j hij
      change ((d.trans hij).app _).left â‰« _ â‰« _ = _
      conv_rhs => rw [â† colimit.w d.relativeGluingData.functor hij]
      simp_rw [â† Category.assoc]
      congr 1
      exact congr($(d.cocone_Î¹_transitionMap hij a).left).symm
    Â· intro
      simp [ğ’±, pullback.condition, glued]
  Â· ext
    apply (ğ’± a).hom_ext
    intro i
    have : (ğ’± a).f i â‰« (D.map f).left =
        ((D â‹™ Over.pullback _ _ (ğ’°.f i)).map f).left â‰« (ğ’± b).f i := by
      simp [ğ’±]
    dsimp
    rw [Category.comp_id, Scheme.OpenCover.map_glueMorphismsOverOfLocallyDirected_left,
      reassoc_of% this, Scheme.OpenCover.map_glueMorphismsOverOfLocallyDirected_left,
      â† Over.comp_left_assoc, â† Comma.comp_hom, â† Functor.comp_map, Cocone.w]

@[reassoc]
lemma fst_gluedCocone_Î¹ (a : J) (i : ğ’°.Iâ‚€) :
    pullback.fst (D.obj a).hom (ğ’°.f i) â‰«
      (d.gluedCocone.Î¹.app a).left =
      ((d.cocone i).Î¹.app a).left â‰« colimit.Î¹ d.relativeGluingData.functor i := by
  simp only [gluedCocone]
  generalize_proofs _ _ _ _ _ _ _ _ _ _ _ _ h1 h2
  let ğ’± : (D.obj a).left.OpenCover := ğ’°.pullbackâ‚ (D.obj a).hom
  apply ğ’±.map_glueMorphismsOverOfLocallyDirected_left _ h1 h2

/-- The glued cocone is colimiting. -/
noncomputable
def isColimitGluedCocone : IsColimit d.gluedCocone := by
  letI ğ’± : d.glued.left.OpenCover := d.relativeGluingData.cover
  refine { desc s := ?_, fac := ?_, uniq := ?_ }
  Â· refine âŸ¨ğ’±.glueMorphismsOverOfLocallyDirected ?_ ?_ ?_, trivial, trivialâŸ©
    Â· intro i
      exact ((d.isColimit i).desc (Functor.mapCocone _ s)).left â‰« pullback.fst _ _
    Â· intro i j hij
      simp only [Functor.mapCocone_pt, MorphismProperty.Over.pullback_obj_left,
        Functor.id_obj, Functor.const_obj_obj]
      change (d.transitionMap hij).left â‰« _ â‰« _ = _
      have :
        d.transitionMap hij â‰«
          (d.isColimit j).desc ((Over.pullback P âŠ¤ (ğ’°.f j)).mapCocone s) =
            (Over.map âŠ¤ (d.prop_trans hij)).map
            ((d.isColimit i).desc ((MorphismProperty.Over.pullback P âŠ¤ (ğ’°.f i)).mapCocone s)) â‰«
            (Over.pullbackMapHomPullback _ (d.prop_trans hij) trivial _ _).app _ := by
        dsimp [transitionMap]
        apply (isColimitOfPreserves (MorphismProperty.Over.map âŠ¤ _) (d.isColimit i)).hom_ext
        intro a
        rw [IsColimit.fac_assoc]
        simp only [Functor.comp_obj, transitionCocone, Cocones.precompose_obj_pt,
          Cocones.precompose_obj_Î¹, NatTrans.comp_app, Functor.const_obj_obj, Category.assoc,
          IsColimit.fac, Functor.mapCocone_pt, Functor.mapCocone_Î¹_app]
        rw [â† Functor.map_comp_assoc, IsColimit.fac]
        ext
        apply pullback.hom_ext <;> simp
      rw [â† Over.comp_left_assoc, â† Comma.comp_hom, this]
      simp
    Â· intro i
      have := Over.w ((d.isColimit i).desc (Functor.mapCocone _ s))
      dsimp at this
      dsimp
      simp only [glued, Category.assoc, Scheme.IsLocallyDirected.openCover_f, ğ’±]
      rw [pullback.condition, reassoc_of% this]
      simp
  Â· intro s a
    letI ğ’² (a : J) : (D.obj a).left.OpenCover := ğ’°.pullbackâ‚ (D.obj a).hom
    ext
    apply (ğ’² a).hom_ext
    intro i
    dsimp
    simp only [Functor.const_obj_obj, Precoverage.ZeroHypercover.pullbackâ‚_toPreZeroHypercover,
      PreZeroHypercover.pullbackâ‚_X, PreZeroHypercover.pullbackâ‚_f, ğ’²]
    rw [fst_gluedCocone_Î¹_assoc]
    change _ â‰« ğ’±.f _ â‰« _ = _
    rw [Scheme.OpenCover.map_glueMorphismsOverOfLocallyDirected_left, â† Over.comp_left_assoc,
      â† Comma.comp_hom, IsColimit.fac]
    simp
  Â· intro s m hm
    ext
    refine ğ’±.hom_ext _ _ fun i â†¦ ?_
    have : (d.pullbackGluedIso i).inv â‰« (Over.pullback P âŠ¤ (ğ’°.f i)).map m =
        (d.isColimit i).desc ((Over.pullback P âŠ¤ (ğ’°.f i)).mapCocone s) := by
      refine (d.isColimit i).hom_ext fun a â†¦ ?_
      simp only [IsColimit.fac, Functor.mapCocone_pt, Functor.mapCocone_Î¹_app]
      ext
      apply pullback.hom_ext
      Â· dsimp
        simp only [Category.assoc, limit.lift_Ï€, PullbackCone.mk_Ï€_app,
          pullbackGluedIso_inv_fst_assoc]
        rw [â† congr($(hm a).left)]
        dsimp
        rw [fst_gluedCocone_Î¹_assoc]
      Â· simp
    rw [Scheme.OpenCover.map_glueMorphismsOverOfLocallyDirected_left]
    simp [ğ’±, â† this]

end ColimitGluingData

lemma hasColimit_of_locallyDirected
    (H : âˆ€ {i j} (hij : i âŸ¶ j), P (ğ’°.trans hij))
    [âˆ€ {i j : ğ’°.Iâ‚€} (hij : i âŸ¶ j), PreservesColimitsOfShape J (Over.map âŠ¤ (H hij))]
    [âˆ€ {i j : ğ’°.Iâ‚€} (hij : i âŸ¶ j), PreservesColimitsOfShape J (Over.pullback P âŠ¤ (ğ’°.trans hij))]
    [âˆ€ i, HasColimit (D â‹™ Over.pullback _ _ (ğ’°.f i))]
    [Quiver.IsThin ğ’°.Iâ‚€] [Small.{u} ğ’°.Iâ‚€] [IsZariskiLocalAtTarget P] :
    HasColimit D :=
  let d : ColimitGluingData D ğ’° :=
    { cocone _ := colimit.cocone _
      isColimit _ := colimit.isColimit _
      prop_trans := H  }
  âŸ¨d.gluedCocone, d.isColimitGluedCoconeâŸ©

end AlgebraicGeometry.Scheme.Cover
