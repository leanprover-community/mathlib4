/-
Copyright (c) 2025 Brian Nugent. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Brian Nugent
-/

import Mathlib.AlgebraicGeometry.Noetherian
import Mathlib.RingTheory.HopkinsLevitzki

/-!
# Artinian and Locally Artinian Schemes

We define and prove basic properties about Artinian and Locally Artinian Schemes.

## Main definitions

* `AlgebraicGeometry.IsLocallyArtinian`: A scheme is locally Artinian if for all open affines,
  the section ring is an Artinian ring.

* `AlgebraicGeometry.IsArtinianScheme`: A scheme is Artinian if it is locally Artinian and
  quasi-compact.

## Main results

* `AlgebraicGeometry.IsLocallyArtinian_iff_IsLocallyNoetherian_and_DiscreteTopology`: A scheme is
  locally Artinian if and only if it is LocallyNoetherian and it has the discrete topology.

* `AlgebraicGeometry.IsArtinianScheme_iff_IsNoetherian_and_Finite`: A scheme is Artinian if and
  only if it is Noetherian and has the discrete topology.

* `IsLocallyArtinian_Opens_IsLocallyArtinian`: An open subset of a Locally Artinian scheme is
  locally Artinian (when considered as a scheme in the natural way)

* `inst_IsArtinianScheme_Finite`: An Artinian scheme is finite.

* `AlgebraicGeometry.IsArtinianRing_iff_IsArtinianScheme`: A commutative ring R is Artinian if
  and only if Spec R is Artinian.

TODO: Show that all Artinian schemes are affine.

-/

noncomputable section

namespace AlgebraicGeometry

variable (X : Scheme)

/-- A scheme `X` is locally Artinian if `ð’ªâ‚“(U)` is Artinian for all affine `U`. -/
class IsLocallyArtinian : Prop where
  component_artinian : âˆ€ (U : X.affineOpens),
    IsArtinianRing Î“(X, U) := by infer_instance

lemma IsLocallyArtinian_IsLocallyNoetherian :
    IsLocallyArtinian X â†’ IsLocallyNoetherian X := fun hA =>
  {component_noetherian := fun U =>
    by
      have _ := hA.1 U
      infer_instance}

lemma IsArtinianRing_DiscreteTopology (R : Type*) [CommRing R] [IsArtinianRing R] :
    DiscreteTopology (PrimeSpectrum R) := by
  apply DiscreteTopology.of_finite_of_isClosed_singleton
  intro p
  apply (PrimeSpectrum.isClosed_singleton_iff_isMaximal p).mpr
  exact Ideal.isMaximal_of_isPrime p.asIdeal

instance IsLocallyArtinian_IsAffine_IsArtinianRing [h : IsLocallyArtinian X] [IsAffine X] :
    IsArtinianRing Î“(X, âŠ¤) :=
  h.1 âŸ¨âŠ¤, isAffineOpen_top XâŸ©

lemma IsLocallyArtinian_IsAffine_DiscreteTopology [IsLocallyArtinian X] [IsAffine X] :
    DiscreteTopology X := by
  have F := AlgebraicGeometry.Scheme.isoSpec X
  apply (Homeomorph.discreteTopology_iff (AlgebraicGeometry.Scheme.Hom.homeomorph F.hom)).mpr
  exact IsArtinianRing_DiscreteTopology Î“(X,âŠ¤)

instance IsLocallyArtinian_Opens_IsLocallyArtinian [h : IsLocallyArtinian X] {U : X.Opens} :
    IsLocallyArtinian U := by
  refine { component_artinian := ?_ }
  intro W
  have F := (Scheme.Hom.appIso U.Î¹ â†‘W).commRingCatIsoToRingEquiv
  have _ : IsArtinianRing Î“(X, U.Î¹ ''áµ W) :=
    h.1 âŸ¨(U.Î¹ ''áµ W), AlgebraicGeometry.IsAffineOpen.image_of_isOpenImmersion W.2 U.Î¹âŸ©
  exact RingEquiv.isArtinianRing F

lemma IsLocallyArtinian_DiscreteTopology :
    IsLocallyArtinian X â†’ DiscreteTopology X := by
  intro hA
  apply discreteTopology_iff_isOpen_singleton.mpr
  intro x
  have : x âˆˆ (âŠ¤ : X.Opens) := trivial
  obtain âŸ¨W, hW1, hW2, _âŸ© := exists_isAffineOpen_mem_and_subset this
  have _ : IsAffine W := hW1
  have : DiscreteTopology W := IsLocallyArtinian_IsAffine_DiscreteTopology W
  have : IsOpen ({(âŸ¨x, hW2âŸ©)} : Set W) := by
    apply discreteTopology_iff_forall_isOpen.mp
    exact IsLocallyArtinian_IsAffine_DiscreteTopology W
  have _ := IsOpen.trans this W.2
  have : Subtype.val '' {âŸ¨x, hW2âŸ©} = {x} := Set.image_singleton
  rw[â† this]
  assumption

instance inst_IsLocallyArtinian_DiscreteTopology [h : IsLocallyArtinian X] :
    DiscreteTopology X :=
  IsLocallyArtinian_DiscreteTopology X h

theorem IsNoetherianRing_DiscreteTopololgy_IsArtinianRing
(R : Type*) [CommRing R] [IsNoetherianRing R] [DiscreteTopology (PrimeSpectrum R)] :
    IsArtinianRing R := by
  apply isArtinianRing_iff_krullDimLE_zero.mpr
  apply Ring.krullDimLE_zero_iff.mpr
  intro I hI
  let p : PrimeSpectrum R := âŸ¨I, hIâŸ©
  apply (PrimeSpectrum.isClosed_singleton_iff_isMaximal p).mp
  exact isClosed_singleton

lemma IsLocallyNoetherian_DiscreteTopology_IsLocallyArtinian
[IsLocallyNoetherian X] [DiscreteTopology X] :
    IsLocallyArtinian X := by
  refine { component_artinian := ?_ }
  intro U
  have _ : IsNoetherianRing Î“(X,U) := IsLocallyNoetherian.component_noetherian U
  have _ : DiscreteTopology (PrimeSpectrum Î“(X,U)) := by
    change DiscreteTopology (Spec Î“(X,U))
    have F := AlgebraicGeometry.IsAffineOpen.isoSpec U.2
    apply (Homeomorph.discreteTopology_iff (AlgebraicGeometry.Scheme.Hom.homeomorph F.hom)).mp
    exact instDiscreteTopologySubtype
  exact IsNoetherianRing_DiscreteTopololgy_IsArtinianRing Î“(X, U)

theorem IsLocallyArtinian_iff_IsLocallyNoetherian_and_DiscreteTopology :
    IsLocallyArtinian X â†” IsLocallyNoetherian X âˆ§ DiscreteTopology X :=
  âŸ¨fun h => âŸ¨IsLocallyArtinian_IsLocallyNoetherian X h, IsLocallyArtinian_DiscreteTopology X hâŸ©,
  fun âŸ¨_,_âŸ© => IsLocallyNoetherian_DiscreteTopology_IsLocallyArtinian XâŸ©

instance inst_IsLocallyArtinian_IsLocallyNoetherian [IsLocallyArtinian X] :
    IsLocallyNoetherian X := IsLocallyArtinian_IsLocallyNoetherian X inferInstance

@[mk_iff]
class IsArtinianScheme : Prop extends IsLocallyArtinian X, CompactSpace X

instance inst_IsArtinianScheme_Finite [h : IsArtinianScheme X] :
    Finite X := @finite_of_compact_of_discrete X _ _ _

instance inst_IsArtinianScheme_IsNoetherianScheme [IsArtinianScheme X] :
    IsNoetherian X :=
      { toIsLocallyNoetherian := inferInstance,
        toCompactSpace := inferInstance}

theorem IsArtinianScheme_iff_IsNoetherian_and_DiscreteTopology :
    IsArtinianScheme X â†” IsNoetherian X âˆ§ DiscreteTopology X :=
  âŸ¨fun _ => âŸ¨inferInstance, inferInstanceâŸ©,
  fun âŸ¨_,_âŸ© =>
    {toIsLocallyArtinian := IsLocallyNoetherian_DiscreteTopology_IsLocallyArtinian X,
      toCompactSpace := inferInstance}âŸ©

/-- A commutative ring R is Artinian if and only if Spec R is and Artinian scheme -/
theorem IsArtinianRing_iff_IsArtinianScheme (R : Type*) [CommRing R] :
    IsArtinianRing R â†” IsArtinianScheme (Spec (CommRingCat.of R)) := by
  constructor
  Â· intro _
    apply (IsArtinianScheme_iff_IsNoetherian_and_DiscreteTopology (Spec (CommRingCat.of R))).mpr
    exact âŸ¨inferInstance, IsArtinianRing_DiscreteTopology RâŸ©
  intro _
  have F := (AlgebraicGeometry.Scheme.Î“SpecIso (CommRingCat.of R)).commRingCatIsoToRingEquiv
  exact RingEquiv.isArtinianRing F

end AlgebraicGeometry
