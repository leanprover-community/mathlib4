/-
Copyright (c) 2025 Brian Nugent. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Brian Nugent
-/
module

public import Mathlib.AlgebraicGeometry.Noetherian
public import Mathlib.AlgebraicGeometry.Morphisms.Immersion
public import Mathlib.RingTheory.HopkinsLevitzki

/-!
# Artinian and Locally Artinian Schemes

We define and prove basic properties about Artinian and locally Artinian Schemes.

## Main definitions

* `AlgebraicGeometry.IsLocallyArtinian`: A scheme is locally Artinian if for all open affines,
  the section ring is an Artinian ring.

* `AlgebraicGeometry.IsArtinianScheme`: A scheme is Artinian if it is locally Artinian and
  quasi-compact.

## Main results

* `AlgebraicGeometry.IsLocallyArtinian.iff_isLocallyNoetherian_and_discreteTopology`: A scheme is
  locally Artinian if and only if it is LocallyNoetherian and it has the discrete topology.

* `AlgebraicGeometry.IsArtinianScheme.iff_isNoetherian_and_discreteTopology`: A scheme is Artinian
  if and only if it is Noetherian and has the discrete topology.

* `AlgebraicGeometry.IsArtinianScheme.finite`: An Artinian scheme is finite.

* `AlgebraicGeometry.Scheme.isArtinianRing_iff_IsArtinianScheme`: A commutative ring R is Artinian
  if and only if Spec R is Artinian.

TODO(Brian-Nugent): Show that all Artinian schemes are affine.

-/

@[expose] public section

noncomputable section

open CategoryTheory

universe u

namespace AlgebraicGeometry

variable {X Y : Scheme.{u}} (f : X ‚ü∂ Y)

/-- A scheme `X` is locally Artinian if `ùí™‚Çì(U)` is Artinian for all affine `U`. -/
class IsLocallyArtinian (X : Scheme) : Prop where
  isArtinianRing_presheaf_obj : ‚àÄ (U : X.affineOpens),
    IsArtinianRing Œì(X, U) := by infer_instance

attribute [instance] IsLocallyArtinian.isArtinianRing_presheaf_obj

instance IsLocallyArtinian.isLocallyNoetherian [h : IsLocallyArtinian X] :
    IsLocallyNoetherian X where

instance IsLocallyArtinian.isArtinianRing_of_isAffine [h : IsLocallyArtinian X] [IsAffine X] :
    IsArtinianRing Œì(X, ‚ä§) :=
  h.1 ‚ü®‚ä§, isAffineOpen_top X‚ü©

lemma IsLocallyArtinian.of_topologicalKrullDim_le_zero
    [IsLocallyNoetherian X] (h : topologicalKrullDim X ‚â§ 0) : IsLocallyArtinian X where
  isArtinianRing_presheaf_obj U := by
    have _ : IsNoetherianRing Œì(X, U) := IsLocallyNoetherian.component_noetherian U
    rw [isArtinianRing_iff_krullDimLE_zero, Ring.KrullDimLE, Order.krullDimLE_iff, ‚Üê ringKrullDim,
      Nat.cast_zero, ‚Üê PrimeSpectrum.topologicalKrullDim_eq_ringKrullDim Œì(X, U)]
    change topologicalKrullDim (Spec Œì(X, U)) ‚â§ 0
    rw [‚Üê IsHomeomorph.topologicalKrullDim_eq _ U.2.isoSpec.hom.homeomorph.isHomeomorph]
    exact (topologicalKrullDim_subspace_le X U).trans h

theorem IsLocallyArtinian.of_isLocallyNoetherian_of_discreteTopology
    [IsLocallyNoetherian X] [DiscreteTopology X] :
    IsLocallyArtinian X :=
  .of_topologicalKrullDim_le_zero (topologicalKrullDim_zero_of_discreteTopology X)

/-- See `isLocallyArtinian_of_isImmersion`. -/
private lemma IsLocallyArtinian.of_isOpenImmersion [IsOpenImmersion f] [IsLocallyArtinian Y] :
    IsLocallyArtinian X where
  isArtinianRing_presheaf_obj U :=
    have : IsArtinianRing ‚ÜëŒì(Y, f ''·µÅ ‚ÜëU) :=
      IsLocallyArtinian.isArtinianRing_presheaf_obj ‚ü®_, U.2.image_of_isOpenImmersion f‚ü©
    (f.appIso U).commRingCatIsoToRingEquiv.surjective.isArtinianRing

instance [IsLocallyArtinian X] {U : X.Opens} : IsLocallyArtinian U := .of_isOpenImmersion U.Œπ

instance [IsLocallyArtinian X] {U : X.OpenCover} (i) : IsLocallyArtinian (U.X i) :=
  .of_isOpenImmersion (U.f i)

instance (priority := low) IsLocallyArtinian.discreteTopology [IsLocallyArtinian X] :
    DiscreteTopology X := by
  apply discreteTopology_iff_isOpen_singleton.mpr
  intro x
  obtain ‚ü®W, hW1, hW2, _‚ü© := exists_isAffineOpen_mem_and_subset (TopologicalSpace.Opens.mem_top x)
  have : IsArtinianRing Œì(X, W) := IsLocallyArtinian.isArtinianRing_presheaf_obj ‚ü®_, hW1‚ü©
  have : DiscreteTopology (Spec Œì(X, W)) := inferInstanceAs (DiscreteTopology (PrimeSpectrum _))
  have : DiscreteTopology W := hW1.isoSpec.hom.homeomorph.symm.discreteTopology
  simpa using (isOpen_discrete ({‚ü®x, hW2‚ü©} : Set W)).trans W.2

@[deprecated (since := "2026-01-14")]
alias IsLocallyArtinian.discreteTopology_of_isAffine := IsLocallyArtinian.discreteTopology

theorem IsLocallyArtinian.iff_isLocallyNoetherian_and_discreteTopology :
    IsLocallyArtinian X ‚Üî IsLocallyNoetherian X ‚àß DiscreteTopology X :=
  ‚ü®fun _ ‚Ü¶ ‚ü®inferInstance, inferInstance‚ü©, fun ‚ü®_, _‚ü© ‚Ü¶ .of_isLocallyNoetherian_of_discreteTopology‚ü©

-- This can be extended to locally quasi-finite morphisms.
theorem IsLocallyArtinian.of_isImmersion [IsImmersion f] [IsLocallyArtinian Y] :
    IsLocallyArtinian X :=
  iff_isLocallyNoetherian_and_discreteTopology.mpr
    ‚ü®LocallyOfFiniteType.isLocallyNoetherian f, f.isEmbedding.discreteTopology‚ü©

/-- A commutative ring `R` is Artinian if and only if `Spec R` is an Artinian scheme. -/
@[simp] theorem Scheme.isLocallyArtinianScheme_Spec {R : CommRingCat} :
    IsLocallyArtinian (Spec R) ‚Üî IsArtinianRing R where
  mp _ := (AlgebraicGeometry.Scheme.ŒìSpecIso R).commRingCatIsoToRingEquiv.isArtinianRing
  mpr _ := .of_topologicalKrullDim_le_zero
    (topologicalKrullDim_zero_of_discreteTopology (PrimeSpectrum _))

theorem isLocallyArtinian_iff_openCover (ùí∞ : X.OpenCover) :
    IsLocallyArtinian X ‚Üî ‚àÄ (i : ùí∞.I‚ÇÄ), IsLocallyArtinian (ùí∞.X i) := by
  refine ‚ü®fun h ‚Ü¶ inferInstance, fun H ‚Ü¶ ?_‚ü©
  refine IsLocallyArtinian.iff_isLocallyNoetherian_and_discreteTopology.mpr ‚ü®?_, ?_‚ü©
  ¬∑ exact (isLocallyNoetherian_iff_openCover ùí∞).mpr inferInstance
  ¬∑ refine discreteTopology_iff_isOpen_singleton.mpr fun x ‚Ü¶ ?_
    obtain ‚ü®i, x, rfl‚ü© := ùí∞.exists_eq x
    simpa using (ùí∞.f i).isOpenEmbedding.isOpenMap _ (isOpen_discrete {x})

theorem isLocallyArtinian_iff_of_isOpenCover {Œπ : Type*} {U : Œπ ‚Üí X.Opens}
    (hU : TopologicalSpace.IsOpenCover U) (hU' : ‚àÄ i, IsAffineOpen (U i)) :
    IsLocallyArtinian X ‚Üî ‚àÄ i, IsArtinianRing Œì(X, U i) := by
  refine ‚ü®fun _ _ ‚Ü¶ IsLocallyArtinian.isArtinianRing_presheaf_obj ‚ü®_, hU' _‚ü©, fun H ‚Ü¶ ?_‚ü©
  rw [isLocallyArtinian_iff_openCover (X.openCoverOfIsOpenCover U hU)]
  have : ‚àÄ i, IsLocallyArtinian (Spec Œì(X, U i)) := by simpa
  exact fun i ‚Ü¶ .of_isImmersion (hU' _).isoSpec.hom

/-- A scheme is Artinian if it is locally Artinian and quasi-compact -/
@[mk_iff]
class IsArtinianScheme (X : Scheme.{u}) : Prop extends IsLocallyArtinian X, CompactSpace X

/-- The underlying type of an Artinian Scheme is finite -/
instance (priority := low) IsArtinianScheme.finite [IsArtinianScheme X] :
    Finite X := finite_of_compact_of_discrete

instance (priority := low) IsArtinianScheme.isNoetherianScheme [IsArtinianScheme X] :
    IsNoetherian X where

/-- A scheme is Artinian if and only if it is Noetherian and has the discrete topology. -/
theorem IsArtinianScheme.iff_isNoetherian_and_discreteTopology :
    IsArtinianScheme X ‚Üî IsNoetherian X ‚àß DiscreteTopology X := by
  aesop (add simp [isArtinianScheme_iff, isNoetherian_iff,
    IsLocallyArtinian.iff_isLocallyNoetherian_and_discreteTopology])

instance {R : CommRingCat} [IsArtinianRing R] :
    IsArtinianScheme (Spec R) :=
  IsArtinianScheme.iff_isNoetherian_and_discreteTopology.mpr
    ‚ü®inferInstance, inferInstanceAs (DiscreteTopology (PrimeSpectrum R))‚ü©

/-- A commutative ring `R` is Artinian if and only if `Spec R` is an Artinian scheme -/
theorem Scheme.isArtinianScheme_Spec {R : CommRingCat} :
    IsArtinianScheme (Spec R) ‚Üî IsArtinianRing R := by
  simp [isArtinianScheme_iff, inferInstanceAs (CompactSpace (Spec R))]

end AlgebraicGeometry
