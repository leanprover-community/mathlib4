/-
Copyright (c) 2025 Brian Nugent. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Brian Nugent
-/
module

public import Mathlib.AlgebraicGeometry.Noetherian
public import Mathlib.RingTheory.HopkinsLevitzki

/-!
# Artinian and Locally Artinian Schemes

We define and prove basic properties about Artinian and locally Artinian Schemes.

## Main definitions

* `AlgebraicGeometry.IsLocallyArtinian`: A scheme is locally Artinian if for all open affines,
  the section ring is an Artinian ring.

* `AlgebraicGeometry.IsArtinianScheme`: A scheme is Artinian if it is locally Artinian and
  quasi-compact.

## Main results

* `AlgebraicGeometry.IsLocallyArtinian.iff_isLocallyNoetherian_and_discreteTopology`: A scheme is
  locally Artinian if and only if it is LocallyNoetherian and it has the discrete topology.

* `AlgebraicGeometry.IsArtinianScheme.iff_isNoetherian_and_discreteTopology`: A scheme is Artinian
  if andonly if it is Noetherian and has the discrete topology.

* `AlgebraicGeometry.IsArtinianScheme.finite`: An Artinian scheme is finite.

* `AlgebraicGeometry.Scheme.isArtinianRing_iff_IsArtinianScheme`: A commutative ring R is Artinian
  if and only if Spec R is Artinian.

TODO(Brian-Nugent): Show that all Artinian schemes are affine.

-/

@[expose] public section

noncomputable section

namespace AlgebraicGeometry

variable (X : Scheme)

/-- A scheme `X` is locally Artinian if `ð’ªâ‚“(U)` is Artinian for all affine `U`. -/
class IsLocallyArtinian : Prop where
  isArtinianRing_presheaf_obj : âˆ€ (U : X.affineOpens),
    IsArtinianRing Î“(X, U) := by infer_instance

attribute [instance] IsLocallyArtinian.isArtinianRing_presheaf_obj

instance IsLocallyArtinian.isLocallyNoetherian [h : IsLocallyArtinian X] :
    IsLocallyNoetherian X where

instance IsLocallyArtinian.isArtinianRing_of_isAffine [h : IsLocallyArtinian X] [IsAffine X] :
    IsArtinianRing Î“(X, âŠ¤) :=
  h.1 âŸ¨âŠ¤, isAffineOpen_top XâŸ©

lemma IsLocallyArtinian.discreteTopology_of_isAffine [IsLocallyArtinian X] [IsAffine X] :
    DiscreteTopology X := by
  have F := Scheme.isoSpec X
  apply (Homeomorph.discreteTopology_iff (AlgebraicGeometry.Scheme.Hom.homeomorph F.hom)).mpr
  exact inferInstanceAs (DiscreteTopology (PrimeSpectrum Î“(X,âŠ¤)))

instance [h : IsLocallyArtinian X] {U : X.Opens} :
    IsLocallyArtinian U where
  isArtinianRing_presheaf_obj W :=
    have : IsArtinianRing Î“(X, U.Î¹ ''áµ W) :=
      h.1 âŸ¨(U.Î¹ ''áµ W), AlgebraicGeometry.IsAffineOpen.image_of_isOpenImmersion W.2 U.Î¹âŸ©
    RingEquiv.isArtinianRing (Scheme.Hom.appIso U.Î¹ â†‘W).commRingCatIsoToRingEquiv

instance (priority := low) IsLocallyArtinian.discreteTopology [IsLocallyArtinian X] :
    DiscreteTopology X := by
  apply discreteTopology_iff_isOpen_singleton.mpr
  intro x
  obtain âŸ¨W, hW1, hW2, _âŸ© := exists_isAffineOpen_mem_and_subset (TopologicalSpace.Opens.mem_top x)
  have : IsAffine W := hW1
  have : DiscreteTopology W := IsLocallyArtinian.discreteTopology_of_isAffine W
  have : IsOpen ({âŸ¨x, hW2âŸ©} : Set W) := discreteTopology_iff_forall_isOpen.mp
    (IsLocallyArtinian.discreteTopology_of_isAffine W) {âŸ¨x, hW2âŸ©}
  have := this.trans W.2
  rwa [â† show Subtype.val '' {âŸ¨x, hW2âŸ©} = {x} from Set.image_singleton]

lemma IsLocallyArtinian.of_topologicalKrullDim_le_zero
    {X : Scheme} [IsLocallyNoetherian X] (h : topologicalKrullDim X â‰¤ 0) :
    IsLocallyArtinian X := by
  refine { isArtinianRing_presheaf_obj := ?_ }
  intro U
  have _ : IsNoetherianRing Î“(X,U) := IsLocallyNoetherian.component_noetherian U
  apply isArtinianRing_iff_krullDimLE_zero.mpr
  rw [Ring.KrullDimLE, Order.krullDimLE_iff, â† ringKrullDim, Nat.cast_zero,
    â† PrimeSpectrum.topologicalKrullDim_eq_ringKrullDim Î“(X, U)]
  have Ft := Scheme.Hom.homeomorph (AlgebraicGeometry.IsAffineOpen.isoSpec U.2).hom
  change topologicalKrullDim (Spec Î“(X, U)) â‰¤ 0
  rw [â† IsHomeomorph.topologicalKrullDim_eq Ft Ft.isHomeomorph]
  exact (topologicalKrullDim_subspace_le X U).trans h

theorem IsLocallyArtinian.iff_isLocallyNoetherian_and_discreteTopology :
    IsLocallyArtinian X â†” IsLocallyNoetherian X âˆ§ DiscreteTopology X :=
  âŸ¨fun _ â†¦ âŸ¨inferInstance, inferInstanceâŸ©,
  fun âŸ¨_,_âŸ© â†¦
    have h : topologicalKrullDim X â‰¤ 0 := topologicalKrullDim_zero_of_discreteTopology X
    IsLocallyArtinian.of_topologicalKrullDim_le_zero hâŸ©

/-- A scheme is Artinian if it is locally Artinian and quasi-compact -/
@[mk_iff]
class IsArtinianScheme : Prop extends IsLocallyArtinian X, CompactSpace X

/-- The underlying type of an Artinian Scheme is finite -/
instance IsArtinianScheme.finite [IsArtinianScheme X] :
    Finite X := finite_of_compact_of_discrete

instance IsArtinianScheme.isNoetherianScheme [IsArtinianScheme X] :
    IsNoetherian X where

/-- A scheme is Artinian if and only if it is Noetherian and has the discrete topology. -/
theorem IsArtinianScheme.iff_isNoetherian_and_discreteTopology :
    IsArtinianScheme X â†” IsNoetherian X âˆ§ DiscreteTopology X :=
  âŸ¨fun _ => âŸ¨inferInstance, inferInstanceâŸ©,
  fun âŸ¨_,_âŸ© =>
    {toIsLocallyArtinian := (IsLocallyArtinian.iff_isLocallyNoetherian_and_discreteTopology X).mpr
      âŸ¨inferInstance, inferInstanceâŸ©,
      toCompactSpace := inferInstance}âŸ©

/-- A commutative ring `R` is Artinian if and only if `Spec R` is an Artinian scheme -/
theorem Scheme.isArtinianRing_iff_isArtinianScheme (R : Type*) [CommRing R] :
    IsArtinianRing R â†” IsArtinianScheme (Spec (CommRingCat.of R)) :=
  âŸ¨fun _ â†¦ (IsArtinianScheme.iff_isNoetherian_and_discreteTopology (Spec (CommRingCat.of R))).mpr
    âŸ¨inferInstance, inferInstanceAs (DiscreteTopology (PrimeSpectrum R))âŸ©,
  fun _ => RingEquiv.isArtinianRing
    (AlgebraicGeometry.Scheme.Î“SpecIso (CommRingCat.of R)).commRingCatIsoToRingEquivâŸ©

end AlgebraicGeometry
