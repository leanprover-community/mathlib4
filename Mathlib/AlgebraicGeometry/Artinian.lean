/-
Copyright (c) 2025 Brian Nugent. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Brian Nugent
-/
module

public import Mathlib.AlgebraicGeometry.Noetherian
public import Mathlib.RingTheory.HopkinsLevitzki

/-!
# Artinian and Locally Artinian Schemes

We define and prove basic properties about Artinian and locally Artinian Schemes.

## Main definitions

* `AlgebraicGeometry.IsLocallyArtinian`: A scheme is locally Artinian if for all open affines,
  the section ring is an Artinian ring.

* `AlgebraicGeometry.IsArtinianScheme`: A scheme is Artinian if it is locally Artinian and
  quasi-compact.

## Main results

* `AlgebraicGeometry.IsLocallyArtinian.iff_isLocallyNoetherian_and_discreteTopology`: A scheme is
  locally Artinian if and only if it is LocallyNoetherian and it has the discrete topology.

* `AlgebraicGeometry.IsArtinianScheme.iff_isNoetherian_and_discreteTopology`: A scheme is Artinian
  if andonly if it is Noetherian and has the discrete topology.

* `AlgebraicGeometry.IsArtinianScheme.finite`: An Artinian scheme is finite.

* `AlgebraicGeometry.Scheme.isArtinianRing_iff_IsArtinianScheme`: A commutative ring R is Artinian
  if and only if Spec R is Artinian.

TODO(Brian-Nugent): Show that all Artinian schemes are affine.

-/

@[expose] public section

noncomputable section

namespace AlgebraicGeometry

variable (X : Scheme)

/-- A scheme `X` is locally Artinian if `ùí™‚Çì(U)` is Artinian for all affine `U`. -/
class IsLocallyArtinian : Prop where
  isArtinianRing_presheaf_obj : ‚àÄ (U : X.affineOpens),
    IsArtinianRing Œì(X, U) := by infer_instance

attribute [instance] IsLocallyArtinian.isArtinianRing_presheaf_obj

instance IsLocallyArtinian.isLocallyNoetherian [h : IsLocallyArtinian X] :
    IsLocallyNoetherian X where

instance IsLocallyArtinian.isArtinianRing_of_isAffine [h : IsLocallyArtinian X] [IsAffine X] :
    IsArtinianRing Œì(X, ‚ä§) :=
  h.1 ‚ü®‚ä§, isAffineOpen_top X‚ü©

lemma IsLocallyArtinian.discreteTopology_of_isAffine [IsLocallyArtinian X] [IsAffine X] :
    DiscreteTopology X := by
  have F := Scheme.isoSpec X
  apply (Homeomorph.discreteTopology_iff (AlgebraicGeometry.Scheme.Hom.homeomorph F.hom)).mpr
  exact inferInstanceAs (DiscreteTopology (PrimeSpectrum Œì(X,‚ä§)))

instance [h : IsLocallyArtinian X] {U : X.Opens} :
    IsLocallyArtinian U where
  isArtinianRing_presheaf_obj W :=
    have : IsArtinianRing Œì(X, U.Œπ ''·µÅ W) :=
      h.1 ‚ü®(U.Œπ ''·µÅ W), AlgebraicGeometry.IsAffineOpen.image_of_isOpenImmersion W.2 U.Œπ‚ü©
    RingEquiv.isArtinianRing (Scheme.Hom.appIso U.Œπ ‚ÜëW).commRingCatIsoToRingEquiv

instance (priority := low) IsLocallyArtinian.discreteTopology [IsLocallyArtinian X] :
    DiscreteTopology X := by
  apply discreteTopology_iff_isOpen_singleton.mpr
  intro x
  obtain ‚ü®W, hW1, hW2, _‚ü© := exists_isAffineOpen_mem_and_subset (TopologicalSpace.Opens.mem_top x)
  have : IsAffine W := hW1
  have : DiscreteTopology W := IsLocallyArtinian.discreteTopology_of_isAffine W
  have : IsOpen ({‚ü®x, hW2‚ü©} : Set W) := discreteTopology_iff_forall_isOpen.mp
    (IsLocallyArtinian.discreteTopology_of_isAffine W) {‚ü®x, hW2‚ü©}
  have := this.trans W.2
  rwa [‚Üê show Subtype.val '' {‚ü®x, hW2‚ü©} = {x} from Set.image_singleton]

lemma IsLocallyArtinian.of_topologicalKrullDim_le_zero
    {X : Scheme} [IsLocallyNoetherian X] (h : topologicalKrullDim X ‚â§ 0) :
    IsLocallyArtinian X := by
  refine { isArtinianRing_presheaf_obj := ?_ }
  intro U
  have _ : IsNoetherianRing Œì(X,U) := IsLocallyNoetherian.component_noetherian U
  apply isArtinianRing_iff_krullDimLE_zero.mpr
  rw [Ring.KrullDimLE, Order.krullDimLE_iff, ‚Üê ringKrullDim, Nat.cast_zero,
    ‚Üê PrimeSpectrum.topologicalKrullDim_eq_ringKrullDim Œì(X, U)]
  have Ft := Scheme.Hom.homeomorph (AlgebraicGeometry.IsAffineOpen.isoSpec U.2).hom
  change topologicalKrullDim (Spec Œì(X, U)) ‚â§ 0
  rw [‚Üê IsHomeomorph.topologicalKrullDim_eq Ft Ft.isHomeomorph]
  exact (topologicalKrullDim_subspace_le X U).trans h

theorem IsLocallyArtinian.iff_isLocallyNoetherian_and_discreteTopology :
    IsLocallyArtinian X ‚Üî IsLocallyNoetherian X ‚àß DiscreteTopology X :=
  ‚ü®fun _ ‚Ü¶ ‚ü®inferInstance, inferInstance‚ü©,
  fun ‚ü®_,_‚ü© ‚Ü¶
    have h : topologicalKrullDim X ‚â§ 0 := topologicalKrullDim_zero_of_discreteTopology X
    IsLocallyArtinian.of_topologicalKrullDim_le_zero h‚ü©

/-- A scheme is Artinian if it is locally Artinian and quasi-compact -/
@[mk_iff]
class IsArtinianScheme : Prop extends IsLocallyArtinian X, CompactSpace X

/-- The underlying type of an Artinian Scheme is finite -/
instance IsArtinianScheme.finite [IsArtinianScheme X] :
    Finite X := finite_of_compact_of_discrete

instance IsArtinianScheme.isNoetherianScheme [IsArtinianScheme X] :
    IsNoetherian X where

/-- A scheme is Artinian if and only if it is Noetherian and has the discrete topology. -/
theorem IsArtinianScheme.iff_isNoetherian_and_discreteTopology :
    IsArtinianScheme X ‚Üî IsNoetherian X ‚àß DiscreteTopology X :=
  ‚ü®fun _ => ‚ü®inferInstance, inferInstance‚ü©,
  fun ‚ü®_,_‚ü© =>
    {toIsLocallyArtinian := (IsLocallyArtinian.iff_isLocallyNoetherian_and_discreteTopology X).mpr
      ‚ü®inferInstance, inferInstance‚ü©,
      toCompactSpace := inferInstance}‚ü©

instance {R : CommRingCat} [IsArtinianRing R] :
    IsArtinianScheme (Spec R) :=
  (IsArtinianScheme.iff_isNoetherian_and_discreteTopology (Spec R)).mpr
    ‚ü®inferInstance, inferInstanceAs (DiscreteTopology (PrimeSpectrum R))‚ü©

/-- A commutative ring `R` is Artinian if and only if `Spec R` is an Artinian scheme -/
theorem Scheme.isArtinianScheme_Spec {R : CommRingCat} :
    IsArtinianScheme (Spec R) ‚Üî IsArtinianRing R :=
  ‚ü®fun _ ‚Ü¶ RingEquiv.isArtinianRing (AlgebraicGeometry.Scheme.ŒìSpecIso R).commRingCatIsoToRingEquiv,
  fun _ ‚Ü¶ inferInstance‚ü©

end AlgebraicGeometry
