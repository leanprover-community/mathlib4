/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.Basic
import Mathlib.Topology.Spectral.Hom
import Mathlib.AlgebraicGeometry.Limits

#align_import algebraic_geometry.morphisms.quasi_compact from "leanprover-community/mathlib"@"5dc6092d09e5e489106865241986f7f2ad28d4c8"

/-!
# Quasi-compact morphisms

A morphism of schemes is quasi-compact if the preimages of quasi-compact open sets are
quasi-compact.

It suffices to check that preimages of affine open sets are compact
(`quasiCompact_iff_forall_affine`).

-/


noncomputable section

open CategoryTheory CategoryTheory.Limits Opposite TopologicalSpace

universe u

open scoped AlgebraicGeometry

namespace AlgebraicGeometry

variable {X Y : Scheme.{u}} (f : X ‚ü∂ Y)

/--
A morphism is "quasi-compact" if the underlying map of topological spaces is, i.e. if the preimages
of quasi-compact open sets are quasi-compact.
-/
@[mk_iff]
class QuasiCompact (f : X ‚ü∂ Y) : Prop where
  /-- Preimage of compact open set under a quasi-compact morphism between schemes is compact. -/
  isCompact_preimage : ‚àÄ U : Set Y.carrier, IsOpen U ‚Üí IsCompact U ‚Üí IsCompact (f.1.base ‚Åª¬π' U)
#align algebraic_geometry.quasi_compact AlgebraicGeometry.QuasiCompact

theorem quasiCompact_iff_spectral : QuasiCompact f ‚Üî IsSpectralMap f.1.base :=
  ‚ü®fun ‚ü®h‚ü© => ‚ü®by continuity, h‚ü©, fun h => ‚ü®h.2‚ü©‚ü©
#align algebraic_geometry.quasi_compact_iff_spectral AlgebraicGeometry.quasiCompact_iff_spectral

/-- The `AffineTargetMorphismProperty` corresponding to `QuasiCompact`, asserting that the
domain is a quasi-compact scheme. -/
def QuasiCompact.affineProperty : AffineTargetMorphismProperty := fun X _ _ _ =>
  CompactSpace X.carrier
#align algebraic_geometry.quasi_compact.affine_property AlgebraicGeometry.QuasiCompact.affineProperty

instance (priority := 900) quasiCompactOfIsIso {X Y : Scheme} (f : X ‚ü∂ Y) [IsIso f] :
    QuasiCompact f := by
  constructor
  intro U _ hU'
  convert hU'.image (inv f.1.base).continuous_toFun using 1
  rw [Set.image_eq_preimage_of_inverse]
  ¬∑ delta Function.LeftInverse
    exact IsIso.inv_hom_id_apply f.1.base
  ¬∑ exact IsIso.hom_inv_id_apply f.1.base
#align algebraic_geometry.quasi_compact_of_is_iso AlgebraicGeometry.quasiCompactOfIsIso

instance quasiCompactComp {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [QuasiCompact f]
    [QuasiCompact g] : QuasiCompact (f ‚â´ g) := by
  constructor
  intro U hU hU'
  rw [Scheme.comp_val_base, TopCat.coe_comp, Set.preimage_comp]
  apply QuasiCompact.isCompact_preimage
  ¬∑ exact Continuous.isOpen_preimage (by
    -- Porting note: `continuity` failed
    -- see https://github.com/leanprover-community/mathlib4/issues/5030
      exact Scheme.Hom.continuous g) _ hU
  apply QuasiCompact.isCompact_preimage <;> assumption
#align algebraic_geometry.quasi_compact_comp AlgebraicGeometry.quasiCompactComp

theorem isCompact_open_iff_eq_finset_affine_union {X : Scheme} (U : Set X.carrier) :
    IsCompact U ‚àß IsOpen U ‚Üî
      ‚àÉ s : Set X.affineOpens, s.Finite ‚àß U = ‚ãÉ (i : X.affineOpens) (_ : i ‚àà s), i := by
  apply Opens.IsBasis.isCompact_open_iff_eq_finite_iUnion
    (fun (U : X.affineOpens) => (U : Opens X.carrier))
  ¬∑ rw [Subtype.range_coe]; exact isBasis_affine_open X
  ¬∑ exact fun i => i.2.isCompact
#align algebraic_geometry.is_compact_open_iff_eq_finset_affine_union AlgebraicGeometry.isCompact_open_iff_eq_finset_affine_union

theorem isCompact_open_iff_eq_basicOpen_union {X : Scheme} [IsAffine X] (U : Set X.carrier) :
    IsCompact U ‚àß IsOpen U ‚Üî
      ‚àÉ s : Set (X.presheaf.obj (op ‚ä§)),
        s.Finite ‚àß U = ‚ãÉ (i : X.presheaf.obj (op ‚ä§)) (_ : i ‚àà s), X.basicOpen i :=
  (isBasis_basicOpen X).isCompact_open_iff_eq_finite_iUnion _
    (fun _ => ((topIsAffineOpen _).basicOpenIsAffine _).isCompact) _
#align algebraic_geometry.is_compact_open_iff_eq_basic_open_union AlgebraicGeometry.isCompact_open_iff_eq_basicOpen_union

theorem quasiCompact_iff_forall_affine :
    QuasiCompact f ‚Üî
      ‚àÄ U : Opens Y.carrier, IsAffineOpen U ‚Üí IsCompact (f.1.base ‚Åª¬π' (U : Set Y.carrier)) := by
  rw [quasiCompact_iff]
  refine ‚ü®fun H U hU => H U U.isOpen hU.isCompact, ?_‚ü©
  intro H U hU hU'
  obtain ‚ü®S, hS, rfl‚ü© := (isCompact_open_iff_eq_finset_affine_union U).mp ‚ü®hU', hU‚ü©
  simp only [Set.preimage_iUnion]
  exact Set.Finite.isCompact_biUnion hS (fun i _ => H i i.prop)
#align algebraic_geometry.quasi_compact_iff_forall_affine AlgebraicGeometry.quasiCompact_iff_forall_affine

@[simp]
theorem QuasiCompact.affineProperty_toProperty {X Y : Scheme} (f : X ‚ü∂ Y) :
    (QuasiCompact.affineProperty : _).toProperty f ‚Üî IsAffine Y ‚àß CompactSpace X.carrier := by
  delta AffineTargetMorphismProperty.toProperty QuasiCompact.affineProperty; simp
#align algebraic_geometry.quasi_compact.affine_property_to_property AlgebraicGeometry.QuasiCompact.affineProperty_toProperty

theorem quasiCompact_iff_affineProperty :
    QuasiCompact f ‚Üî targetAffineLocally QuasiCompact.affineProperty f := by
  rw [quasiCompact_iff_forall_affine]
  trans ‚àÄ U : Y.affineOpens, IsCompact (f.1.base ‚Åª¬π' (U : Set Y.carrier))
  ¬∑ exact ‚ü®fun h U => h U U.prop, fun h U hU => h ‚ü®U, hU‚ü©‚ü©
  apply forall_congr'
  exact fun _ => isCompact_iff_compactSpace
#align algebraic_geometry.quasi_compact_iff_affine_property AlgebraicGeometry.quasiCompact_iff_affineProperty

theorem quasiCompact_eq_affineProperty :
    @QuasiCompact = targetAffineLocally QuasiCompact.affineProperty := by
  ext
  exact quasiCompact_iff_affineProperty _
#align algebraic_geometry.quasi_compact_eq_affine_property AlgebraicGeometry.quasiCompact_eq_affineProperty

theorem isCompact_basicOpen (X : Scheme) {U : Opens X.carrier} (hU : IsCompact (U : Set X.carrier))
    (f : X.presheaf.obj (op U)) : IsCompact (X.basicOpen f : Set X.carrier) := by
  classical
  refine ((isCompact_open_iff_eq_finset_affine_union _).mpr ?_).1
  obtain ‚ü®s, hs, e‚ü© := (isCompact_open_iff_eq_finset_affine_union _).mp ‚ü®hU, U.isOpen‚ü©
  let g : s ‚Üí X.affineOpens := by
    intro V
    use V.1 ‚äì X.basicOpen f
    have : V.1.1 ‚ü∂ U := by
      apply homOfLE; change _ ‚äÜ (U : Set X.carrier); rw [e]
      convert Set.subset_iUnion‚ÇÇ (s := fun (U : X.affineOpens) (_ : U ‚àà s) => (U : Set X.carrier))
        V V.prop using 1
    erw [‚Üê X.toLocallyRingedSpace.toRingedSpace.basicOpen_res this.op]
    exact IsAffineOpen.basicOpenIsAffine V.1.prop _
  haveI : Finite s := hs.to_subtype
  refine ‚ü®Set.range g, Set.finite_range g, ?_‚ü©
  refine (Set.inter_eq_right.mpr
            (SetLike.coe_subset_coe.2 <| RingedSpace.basicOpen_le _ _)).symm.trans ?_
  rw [e, Set.iUnion‚ÇÇ_inter]
  apply le_antisymm <;> apply Set.iUnion‚ÇÇ_subset
  ¬∑ intro i hi
    -- Porting note: had to make explicit the first given parameter to `Set.subset_iUnion‚ÇÇ`
    exact Set.Subset.trans (Set.Subset.rfl : _ ‚â§ g ‚ü®i, hi‚ü©)
      (@Set.subset_iUnion‚ÇÇ _ _ _
        (fun (i : Scheme.affineOpens X) (_ : i ‚àà Set.range g) => (i : Set X.toPresheafedSpace)) _
        (Set.mem_range_self ‚ü®i, hi‚ü©))
  ¬∑ rintro ‚ü®i, hi‚ü© ‚ü®‚ü®j, hj‚ü©, hj'‚ü©
    rw [‚Üê hj']
    refine Set.Subset.trans ?_ (Set.subset_iUnion‚ÇÇ j hj)
    exact Set.Subset.rfl
#align algebraic_geometry.is_compact_basic_open AlgebraicGeometry.isCompact_basicOpen

theorem QuasiCompact.affineProperty_isLocal : (QuasiCompact.affineProperty : _).IsLocal := by
  constructor
  ¬∑ apply AffineTargetMorphismProperty.respectsIso_mk <;> rintro X Y Z e _ _ H
    exacts [@Homeomorph.compactSpace _ _ _ _ H (TopCat.homeoOfIso (asIso e.inv.1.base)), H]
  ¬∑ introv H
    dsimp [affineProperty] at H ‚ä¢
    change CompactSpace ((Opens.map f.val.base).obj (Y.basicOpen r))
    rw [Scheme.preimage_basicOpen f r]
    erw [‚Üê isCompact_iff_compactSpace]
    rw [‚Üê isCompact_univ_iff] at H
    apply isCompact_basicOpen
    exact H
  ¬∑ rintro X Y H f S hS hS'
    rw [‚Üê IsAffineOpen.basicOpen_union_eq_self_iff] at hS
    ¬∑ delta QuasiCompact.affineProperty
      rw [‚Üê isCompact_univ_iff]
      change IsCompact ((Opens.map f.val.base).obj ‚ä§).1
      rw [‚Üê hS]
      dsimp [Opens.map]
      simp only [Opens.iSup_mk, Opens.carrier_eq_coe, Opens.coe_mk, Set.preimage_iUnion]
      exact isCompact_iUnion fun i => isCompact_iff_compactSpace.mpr (hS' i)
    ¬∑ exact topIsAffineOpen _
#align algebraic_geometry.quasi_compact.affine_property_is_local AlgebraicGeometry.QuasiCompact.affineProperty_isLocal

theorem QuasiCompact.affine_openCover_tfae {X Y : Scheme.{u}} (f : X ‚ü∂ Y) :
    List.TFAE
      [QuasiCompact f,
        ‚àÉ (ùí∞ : Scheme.OpenCover.{u} Y) (_ : ‚àÄ i, IsAffine (ùí∞.obj i)),
          ‚àÄ i : ùí∞.J, CompactSpace (pullback f (ùí∞.map i)).carrier,
        ‚àÄ (ùí∞ : Scheme.OpenCover.{u} Y) [‚àÄ i, IsAffine (ùí∞.obj i)] (i : ùí∞.J),
          CompactSpace (pullback f (ùí∞.map i)).carrier,
        ‚àÄ {U : Scheme} (g : U ‚ü∂ Y) [IsAffine U] [IsOpenImmersion g],
          CompactSpace (pullback f g).carrier,
        ‚àÉ (Œπ : Type u) (U : Œπ ‚Üí Opens Y.carrier) (_ : iSup U = ‚ä§) (_ : ‚àÄ i, IsAffineOpen (U i)),
          ‚àÄ i, CompactSpace (f.1.base ‚Åª¬π' (U i).1)] :=
  quasiCompact_eq_affineProperty.symm ‚ñ∏ QuasiCompact.affineProperty_isLocal.affine_openCover_TFAE f
#align algebraic_geometry.quasi_compact.affine_open_cover_tfae AlgebraicGeometry.QuasiCompact.affine_openCover_tfae

theorem QuasiCompact.is_local_at_target : PropertyIsLocalAtTarget @QuasiCompact :=
  quasiCompact_eq_affineProperty.symm ‚ñ∏
    QuasiCompact.affineProperty_isLocal.targetAffineLocallyIsLocal
#align algebraic_geometry.quasi_compact.is_local_at_target AlgebraicGeometry.QuasiCompact.is_local_at_target

theorem QuasiCompact.openCover_tfae {X Y : Scheme.{u}} (f : X ‚ü∂ Y) :
    List.TFAE
      [QuasiCompact f,
        ‚àÉ ùí∞ : Scheme.OpenCover.{u} Y,
          ‚àÄ i : ùí∞.J, QuasiCompact (pullback.snd : (ùí∞.pullbackCover f).obj i ‚ü∂ ùí∞.obj i),
        ‚àÄ (ùí∞ : Scheme.OpenCover.{u} Y) (i : ùí∞.J),
          QuasiCompact (pullback.snd : (ùí∞.pullbackCover f).obj i ‚ü∂ ùí∞.obj i),
        ‚àÄ U : Opens Y.carrier, QuasiCompact (f ‚à£_ U),
        ‚àÄ {U : Scheme} (g : U ‚ü∂ Y) [IsOpenImmersion g],
          QuasiCompact (pullback.snd : pullback f g ‚ü∂ _),
        ‚àÉ (Œπ : Type u) (U : Œπ ‚Üí Opens Y.carrier) (_ : iSup U = ‚ä§), ‚àÄ i, QuasiCompact (f ‚à£_ U i)] :=
  quasiCompact_eq_affineProperty.symm ‚ñ∏
    QuasiCompact.affineProperty_isLocal.targetAffineLocallyIsLocal.openCover_TFAE f
#align algebraic_geometry.quasi_compact.open_cover_tfae AlgebraicGeometry.QuasiCompact.openCover_tfae

theorem quasiCompact_over_affine_iff {X Y : Scheme} (f : X ‚ü∂ Y) [IsAffine Y] :
    QuasiCompact f ‚Üî CompactSpace X.carrier :=
  quasiCompact_eq_affineProperty.symm ‚ñ∏ QuasiCompact.affineProperty_isLocal.affine_target_iff f
#align algebraic_geometry.quasi_compact_over_affine_iff AlgebraicGeometry.quasiCompact_over_affine_iff

theorem compactSpace_iff_quasiCompact (X : Scheme) :
    CompactSpace X.carrier ‚Üî QuasiCompact (terminal.from X) :=
  (quasiCompact_over_affine_iff _).symm
#align algebraic_geometry.compact_space_iff_quasi_compact AlgebraicGeometry.compactSpace_iff_quasiCompact

theorem QuasiCompact.affine_openCover_iff {X Y : Scheme.{u}} (ùí∞ : Scheme.OpenCover.{u} Y)
    [‚àÄ i, IsAffine (ùí∞.obj i)] (f : X ‚ü∂ Y) :
    QuasiCompact f ‚Üî ‚àÄ i, CompactSpace (pullback f (ùí∞.map i)).carrier :=
  quasiCompact_eq_affineProperty.symm ‚ñ∏ QuasiCompact.affineProperty_isLocal.affine_openCover_iff f ùí∞
#align algebraic_geometry.quasi_compact.affine_open_cover_iff AlgebraicGeometry.QuasiCompact.affine_openCover_iff

theorem QuasiCompact.openCover_iff {X Y : Scheme.{u}} (ùí∞ : Scheme.OpenCover.{u} Y) (f : X ‚ü∂ Y) :
    QuasiCompact f ‚Üî ‚àÄ i, QuasiCompact (pullback.snd : pullback f (ùí∞.map i) ‚ü∂ _) :=
  quasiCompact_eq_affineProperty.symm ‚ñ∏
    QuasiCompact.affineProperty_isLocal.targetAffineLocallyIsLocal.openCover_iff f ùí∞
#align algebraic_geometry.quasi_compact.open_cover_iff AlgebraicGeometry.QuasiCompact.openCover_iff

theorem quasiCompact_respectsIso : MorphismProperty.RespectsIso @QuasiCompact :=
  quasiCompact_eq_affineProperty.symm ‚ñ∏
    targetAffineLocally_respectsIso QuasiCompact.affineProperty_isLocal.1
#align algebraic_geometry.quasi_compact_respects_iso AlgebraicGeometry.quasiCompact_respectsIso

instance quasiCompact_isStableUnderComposition :
    MorphismProperty.IsStableUnderComposition @QuasiCompact where
  comp_mem _ _ _ _ := inferInstance
#align algebraic_geometry.quasi_compact_stable_under_composition AlgebraicGeometry.quasiCompact_isStableUnderComposition

theorem QuasiCompact.affineProperty_stableUnderBaseChange :
    QuasiCompact.affineProperty.StableUnderBaseChange := by
  intro X Y S _ _ f g h
  rw [QuasiCompact.affineProperty] at h ‚ä¢
  let ùí∞ := Scheme.Pullback.openCoverOfRight Y.affineCover.finiteSubcover f g
  have : Finite ùí∞.J := by dsimp [ùí∞]; infer_instance
  have : ‚àÄ i, CompactSpace (ùí∞.obj i).carrier := by intro i; dsimp [ùí∞]; infer_instance
  exact ùí∞.compactSpace
#align algebraic_geometry.quasi_compact.affine_property_stable_under_base_change AlgebraicGeometry.QuasiCompact.affineProperty_stableUnderBaseChange

theorem quasiCompact_stableUnderBaseChange : MorphismProperty.StableUnderBaseChange @QuasiCompact :=
  quasiCompact_eq_affineProperty.symm ‚ñ∏
    QuasiCompact.affineProperty_isLocal.stableUnderBaseChange
      QuasiCompact.affineProperty_stableUnderBaseChange
#align algebraic_geometry.quasi_compact_stable_under_base_change AlgebraicGeometry.quasiCompact_stableUnderBaseChange

variable {Z : Scheme.{u}}

instance (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [QuasiCompact g] :
    QuasiCompact (pullback.fst : pullback f g ‚ü∂ X) :=
  quasiCompact_stableUnderBaseChange.fst f g inferInstance

instance (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [QuasiCompact f] :
    QuasiCompact (pullback.snd : pullback f g ‚ü∂ Y) :=
  quasiCompact_stableUnderBaseChange.snd f g inferInstance

@[elab_as_elim]
theorem compact_open_induction_on {P : Opens X.carrier ‚Üí Prop} (S : Opens X.carrier)
    (hS : IsCompact S.1) (h‚ÇÅ : P ‚ä•)
    (h‚ÇÇ : ‚àÄ (S : Opens X.carrier) (_ : IsCompact S.1) (U : X.affineOpens), P S ‚Üí P (S ‚äî U)) :
    P S := by
  classical
  obtain ‚ü®s, hs, hs'‚ü© := (isCompact_open_iff_eq_finset_affine_union S.1).mp ‚ü®hS, S.2‚ü©
  replace hs' : S = iSup fun i : s => (i : Opens X.carrier) := by ext1; simpa using hs'
  subst hs'
  apply @Set.Finite.induction_on _ _ _ hs
  ¬∑ convert h‚ÇÅ; rw [iSup_eq_bot]; rintro ‚ü®_, h‚ü©; exact h.elim
  ¬∑ intro x s _ hs h‚ÇÑ
    have : IsCompact (‚®Ü i : s, (i : Opens X.carrier)).1 := by
      refine ((isCompact_open_iff_eq_finset_affine_union _).mpr ?_).1; exact ‚ü®s, hs, by simp‚ü©
    convert h‚ÇÇ _ this x h‚ÇÑ
    rw [iSup_subtype, sup_comm]
    conv_rhs => rw [iSup_subtype]
    exact iSup_insert
#align algebraic_geometry.compact_open_induction_on AlgebraicGeometry.compact_open_induction_on

theorem exists_pow_mul_eq_zero_of_res_basicOpen_eq_zero_of_isAffineOpen (X : Scheme)
    {U : Opens X} (hU : IsAffineOpen U) (x f : X.presheaf.obj (op U))
    (H : x |_ X.basicOpen f = 0) : ‚àÉ n : ‚Ñï, f ^ n * x = 0 := by
  rw [‚Üê map_zero (X.presheaf.map (homOfLE <| X.basicOpen_le f : X.basicOpen f ‚ü∂ U).op)] at H
  obtain ‚ü®‚ü®_, n, rfl‚ü©, e‚ü© := (hU.isLocalization_basicOpen f).exists_of_eq H
  exact ‚ü®n, by simpa [mul_comm x] using e‚ü©
#align algebraic_geometry.exists_pow_mul_eq_zero_of_res_basic_open_eq_zero_of_is_affine_open AlgebraicGeometry.exists_pow_mul_eq_zero_of_res_basicOpen_eq_zero_of_isAffineOpen

/-- If `x : Œì(X, U)` is zero on `D(f)` for some `f : Œì(X, U)`, and `U` is quasi-compact, then
`f ^ n * x = 0` for some `n`. -/
theorem exists_pow_mul_eq_zero_of_res_basicOpen_eq_zero_of_isCompact (X : Scheme.{u})
    {U : Opens X.carrier} (hU : IsCompact U.1) (x f : X.presheaf.obj (op U))
    (H : x |_ X.basicOpen f = 0) : ‚àÉ n : ‚Ñï, f ^ n * x = 0 := by
  obtain ‚ü®s, hs, e‚ü© := (isCompact_open_iff_eq_finset_affine_union U.1).mp ‚ü®hU, U.2‚ü©
  replace e : U = iSup fun i : s => (i : Opens X.carrier) := by
    ext1; simpa using e
  have h‚ÇÅ : ‚àÄ i : s, i.1.1 ‚â§ U := by
    intro i
    change (i : Opens X.carrier) ‚â§ U
    rw [e]
    -- Porting note: `exact le_iSup _ _` no longer works
    exact le_iSup (fun (i : s) => (i : Opens (X.toPresheafedSpace))) _
  have H' := fun i : s =>
    exists_pow_mul_eq_zero_of_res_basicOpen_eq_zero_of_isAffineOpen X i.1.2
      (X.presheaf.map (homOfLE (h‚ÇÅ i)).op x) (X.presheaf.map (homOfLE (h‚ÇÅ i)).op f) ?_
  swap
  ¬∑ delta TopCat.Presheaf.restrictOpen TopCat.Presheaf.restrict at H ‚ä¢
    convert congr_arg (X.presheaf.map (homOfLE _).op) H
    -- Note: the below was `simp only [‚Üê comp_apply]`
    ¬∑ rw [‚Üê comp_apply, ‚Üê comp_apply]
      ¬∑ simp only [‚Üê Functor.map_comp]
        rfl
      ¬∑ simp only [Scheme.basicOpen_res, ge_iff_le, inf_le_right]
    ¬∑ rw [map_zero]
  choose n hn using H'
  haveI := hs.to_subtype
  cases nonempty_fintype s
  use Finset.univ.sup n
  suffices ‚àÄ i : s, X.presheaf.map (homOfLE (h‚ÇÅ i)).op (f ^ Finset.univ.sup n * x) = 0 by
    subst e
    apply TopCat.Sheaf.eq_of_locally_eq X.sheaf fun i : s => (i : Opens X.carrier)
    intro i
    rw [map_zero]
    apply this
  intro i
  replace hn :=
    congr_arg (fun x => X.presheaf.map (homOfLE (h‚ÇÅ i)).op (f ^ (Finset.univ.sup n - n i)) * x)
      (hn i)
  dsimp at hn
  simp only [‚Üê map_mul, ‚Üê map_pow] at hn
  rwa [mul_zero, ‚Üê mul_assoc, ‚Üê pow_add, tsub_add_cancel_of_le] at hn
  apply Finset.le_sup (Finset.mem_univ i)
#align algebraic_geometry.exists_pow_mul_eq_zero_of_res_basic_open_eq_zero_of_is_compact AlgebraicGeometry.exists_pow_mul_eq_zero_of_res_basicOpen_eq_zero_of_isCompact

end AlgebraicGeometry
