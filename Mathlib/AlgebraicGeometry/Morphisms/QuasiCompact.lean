/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.UnderlyingMap
import Mathlib.Topology.Spectral.Hom
import Mathlib.AlgebraicGeometry.Limits

/-!
# Quasi-compact morphisms

A morphism of schemes is quasi-compact if the preimages of quasi-compact open sets are
quasi-compact.

It suffices to check that preimages of affine open sets are compact
(`quasiCompact_iff_forall_affine`).

-/


noncomputable section

open CategoryTheory CategoryTheory.Limits Opposite TopologicalSpace

universe u

open scoped AlgebraicGeometry

namespace AlgebraicGeometry

variable {X Y : Scheme.{u}} (f : X ‚ü∂ Y)

/--
A morphism is "quasi-compact" if the underlying map of topological spaces is, i.e. if the preimages
of quasi-compact open sets are quasi-compact.
-/
@[mk_iff]
class QuasiCompact (f : X ‚ü∂ Y) : Prop where
  /-- Preimage of compact open set under a quasi-compact morphism between schemes is compact. -/
  isCompact_preimage : ‚àÄ U : Set Y, IsOpen U ‚Üí IsCompact U ‚Üí IsCompact (f.base ‚Åª¬π' U)

theorem quasiCompact_iff_spectral : QuasiCompact f ‚Üî IsSpectralMap f.base :=
  ‚ü®fun ‚ü®h‚ü© => ‚ü®by fun_prop, h‚ü©, fun h => ‚ü®h.2‚ü©‚ü©

instance (priority := 900) quasiCompact_of_isIso {X Y : Scheme} (f : X ‚ü∂ Y) [IsIso f] :
    QuasiCompact f := by
  constructor
  intro U _ hU'
  convert hU'.image (inv f.base).hom.continuous_toFun using 1
  rw [Set.image_eq_preimage_of_inverse]
  ¬∑ delta Function.LeftInverse
    exact IsIso.inv_hom_id_apply f.base
  ¬∑ exact IsIso.hom_inv_id_apply f.base

instance quasiCompact_comp {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [QuasiCompact f]
    [QuasiCompact g] : QuasiCompact (f ‚â´ g) := by
  constructor
  intro U hU hU'
  rw [Scheme.comp_base, TopCat.coe_comp, Set.preimage_comp]
  apply QuasiCompact.isCompact_preimage
  ¬∑ exact Continuous.isOpen_preimage (by fun_prop) _ hU
  apply QuasiCompact.isCompact_preimage <;> assumption

theorem isCompactOpen_iff_eq_finset_affine_union {X : Scheme} (U : Set X) :
    IsCompact U ‚àß IsOpen U ‚Üî ‚àÉ s : Set X.affineOpens, s.Finite ‚àß U = ‚ãÉ i ‚àà s, i := by
  apply Opens.IsBasis.isCompact_open_iff_eq_finite_iUnion
    (fun (U : X.affineOpens) => (U : X.Opens))
  ¬∑ rw [Subtype.range_coe]; exact isBasis_affine_open X
  ¬∑ exact fun i => i.2.isCompact

theorem isCompactOpen_iff_eq_basicOpen_union {X : Scheme} [IsAffine X] (U : Set X) :
    IsCompact U ‚àß IsOpen U ‚Üî
      ‚àÉ s : Set Œì(X, ‚ä§), s.Finite ‚àß U = ‚ãÉ i ‚àà s, X.basicOpen i :=
  (isBasis_basicOpen X).isCompact_open_iff_eq_finite_iUnion _
    (fun _ => ((isAffineOpen_top _).basicOpen _).isCompact) _

theorem quasiCompact_iff_forall_affine :
    QuasiCompact f ‚Üî
      ‚àÄ U : Y.Opens, IsAffineOpen U ‚Üí IsCompact (f ‚Åª¬π·µÅ U : Set X) := by
  rw [quasiCompact_iff]
  refine ‚ü®fun H U hU => H U U.isOpen hU.isCompact, ?_‚ü©
  intro H U hU hU'
  obtain ‚ü®S, hS, rfl‚ü© := (isCompactOpen_iff_eq_finset_affine_union U).mp ‚ü®hU', hU‚ü©
  simp only [Set.preimage_iUnion]
  exact Set.Finite.isCompact_biUnion hS (fun i _ => H i i.prop)

theorem isCompact_basicOpen (X : Scheme) {U : X.Opens} (hU : IsCompact (U : Set X))
    (f : Œì(X, U)) : IsCompact (X.basicOpen f : Set X) := by
  classical
  refine ((isCompactOpen_iff_eq_finset_affine_union _).mpr ?_).1
  obtain ‚ü®s, hs, e‚ü© := (isCompactOpen_iff_eq_finset_affine_union _).mp ‚ü®hU, U.isOpen‚ü©
  let g : s ‚Üí X.affineOpens := by
    intro V
    use V.1 ‚äì X.basicOpen f
    have : V.1.1 ‚ü∂ U := by
      apply homOfLE; change _ ‚äÜ (U : Set X); rw [e]
      convert Set.subset_iUnion‚ÇÇ (s := fun (U : X.affineOpens) (_ : U ‚àà s) => (U : Set X))
        V V.prop using 1
    erw [‚Üê X.toLocallyRingedSpace.toRingedSpace.basicOpen_res this.op]
    exact IsAffineOpen.basicOpen V.1.prop _
  haveI : Finite s := hs.to_subtype
  refine ‚ü®Set.range g, Set.finite_range g, ?_‚ü©
  refine (Set.inter_eq_right.mpr
            (SetLike.coe_subset_coe.2 <| RingedSpace.basicOpen_le _ _)).symm.trans ?_
  rw [e, Set.iUnion‚ÇÇ_inter]
  apply le_antisymm <;> apply Set.iUnion‚ÇÇ_subset
  ¬∑ intro i hi
    -- Porting note: had to make explicit the first given parameter to `Set.subset_iUnion‚ÇÇ`
    exact Set.Subset.trans (Set.Subset.rfl : _ ‚â§ g ‚ü®i, hi‚ü©)
      (@Set.subset_iUnion‚ÇÇ _ _ _
        (fun (i : X.affineOpens) (_ : i ‚àà Set.range g) => (i : Set X.toPresheafedSpace)) _
        (Set.mem_range_self ‚ü®i, hi‚ü©))
  ¬∑ rintro ‚ü®i, hi‚ü© ‚ü®‚ü®j, hj‚ü©, hj'‚ü©
    rw [‚Üê hj']
    refine Set.Subset.trans ?_ (Set.subset_iUnion‚ÇÇ j hj)
    exact Set.Subset.rfl

instance : HasAffineProperty @QuasiCompact (fun X _ _ _ ‚Ü¶ CompactSpace X) where
  eq_targetAffineLocally' := by
    ext X Y f
    simp only [quasiCompact_iff_forall_affine, isCompact_iff_compactSpace, targetAffineLocally,
      Subtype.forall]
    rfl
  isLocal_affineProperty := by
    constructor
    ¬∑ apply AffineTargetMorphismProperty.respectsIso_mk <;> rintro X Y Z e _ _ H
      exacts [@Homeomorph.compactSpace _ _ _ _ H (TopCat.homeoOfIso (asIso e.inv.base)), H]
    ¬∑ introv _ H
      change CompactSpace ((Opens.map f.base).obj (Y.basicOpen r))
      rw [Scheme.preimage_basicOpen f r]
      erw [‚Üê isCompact_iff_compactSpace]
      rw [‚Üê isCompact_univ_iff] at H
      apply isCompact_basicOpen
      exact H
    ¬∑ rintro X Y H f S hS hS'
      rw [‚Üê IsAffineOpen.basicOpen_union_eq_self_iff] at hS
      ¬∑ rw [‚Üê isCompact_univ_iff]
        change IsCompact ((Opens.map f.base).obj ‚ä§).1
        rw [‚Üê hS]
        dsimp [Opens.map]
        simp only [Opens.iSup_mk, Opens.coe_mk, Set.preimage_iUnion]
        exact isCompact_iUnion fun i => isCompact_iff_compactSpace.mpr (hS' i)
      ¬∑ exact isAffineOpen_top _

theorem quasiCompact_over_affine_iff {X Y : Scheme} (f : X ‚ü∂ Y) [IsAffine Y] :
    QuasiCompact f ‚Üî CompactSpace X := by
  rw [HasAffineProperty.iff_of_isAffine (P := @QuasiCompact)]

theorem compactSpace_iff_quasiCompact (X : Scheme) :
    CompactSpace X ‚Üî QuasiCompact (terminal.from X) := by
  rw [HasAffineProperty.iff_of_isAffine (P := @QuasiCompact)]

lemma QuasiCompact.compactSpace_of_compactSpace {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [QuasiCompact f]
    [CompactSpace Y] : CompactSpace X := by
  constructor
  rw [‚Üê Set.preimage_univ (f := f.base)]
  exact QuasiCompact.isCompact_preimage _ isOpen_univ CompactSpace.isCompact_univ

instance quasiCompact_isStableUnderComposition :
    MorphismProperty.IsStableUnderComposition @QuasiCompact where
  comp_mem _ _ _ _ := inferInstance

instance quasiCompact_isStableUnderBaseChange :
    MorphismProperty.IsStableUnderBaseChange @QuasiCompact := by
  letI := HasAffineProperty.isLocal_affineProperty @QuasiCompact
  apply HasAffineProperty.isStableUnderBaseChange
  apply AffineTargetMorphismProperty.IsStableUnderBaseChange.mk
  intro X Y S _ _ f g h
  let ùí∞ := Scheme.Pullback.openCoverOfRight Y.affineCover.finiteSubcover f g
  have : Finite ùí∞.I‚ÇÄ := by dsimp [ùí∞]; infer_instance
  have : ‚àÄ i, CompactSpace (ùí∞.X i) := by intro i; dsimp [ùí∞]; infer_instance
  exact ùí∞.compactSpace

variable {Z : Scheme.{u}}

instance (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [QuasiCompact g] : QuasiCompact (pullback.fst f g) :=
  MorphismProperty.pullback_fst f g inferInstance

instance (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [QuasiCompact f] : QuasiCompact (pullback.snd f g) :=
  MorphismProperty.pullback_snd f g inferInstance

lemma compactSpace_iff_exists :
    CompactSpace X ‚Üî ‚àÉ R, ‚àÉ f : Spec R ‚ü∂ X, Function.Surjective f.base := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun ‚ü®R, f, hf‚ü© ‚Ü¶ ‚ü®hf.range_eq ‚ñ∏ isCompact_range f.continuous‚ü©‚ü©
  let ùí∞ : X.OpenCover := X.affineCover.finiteSubcover
  refine ‚ü®Œì(‚àê ùí∞.X, ‚ä§), (‚àê ùí∞.X).isoSpec.inv ‚â´ Sigma.desc ùí∞.f, ?_‚ü©
  refine Function.Surjective.comp (g := (Sigma.desc ùí∞.f).base)
    (fun x ‚Ü¶ ?_) (‚àê ùí∞.X).isoSpec.inv.surjective
  obtain ‚ü®y, hy‚ü© := ùí∞.covers x
  exact ‚ü®(Sigma.Œπ ùí∞.X (ùí∞.idx x)).base y, by rw [‚Üê Scheme.comp_base_apply, Sigma.Œπ_desc, hy]‚ü©

lemma isCompact_iff_exists {U : X.Opens} :
    IsCompact (U : Set X) ‚Üî ‚àÉ R, ‚àÉ f : Spec R ‚ü∂ X, Set.range f.base = U := by
  refine isCompact_iff_compactSpace.trans ((compactSpace_iff_exists (X := U)).trans ?_)
  refine ‚ü®fun ‚ü®R, f, hf‚ü© ‚Ü¶ ‚ü®R, f ‚â´ U.Œπ, by simp [hf.range_comp]‚ü©, fun ‚ü®R, f, hf‚ü© ‚Ü¶ ?_‚ü©
  refine ‚ü®R, IsOpenImmersion.lift U.Œπ f (by simp [hf]), ?_‚ü©
  rw [‚Üê Set.range_eq_univ]
  apply show Function.Injective (U.Œπ.base '' ¬∑) from Set.image_val_injective
  simp only [Set.image_univ, Scheme.Opens.range_Œπ]
  rwa [‚Üê Set.range_comp, ‚Üê TopCat.coe_comp, ‚Üê Scheme.comp_base, IsOpenImmersion.lift_fac]

@[stacks 01K9]
lemma isClosedMap_iff_specializingMap (f : X ‚ü∂ Y) [QuasiCompact f] :
    IsClosedMap f.base ‚Üî SpecializingMap f.base := by
  refine ‚ü®fun h ‚Ü¶ h.specializingMap, fun H ‚Ü¶ ?_‚ü©
  wlog hY : ‚àÉ R, Y = Spec R
  ¬∑ change topologically @IsClosedMap f
    rw [IsZariskiLocalAtTarget.iff_of_openCover (P := topologically @IsClosedMap) Y.affineCover]
    intro i
    haveI hqc : QuasiCompact (Y.affineCover.pullbackHom f i) :=
        IsZariskiLocalAtTarget.of_isPullback (.of_hasPullback _ _) inferInstance
    refine this (Y.affineCover.pullbackHom f i) ?_ ‚ü®_, rfl‚ü©
    exact IsZariskiLocalAtTarget.of_isPullback
      (P := topologically @SpecializingMap) (.of_hasPullback _ _) H
  obtain ‚ü®S, rfl‚ü© := hY
  clear * - H
  intro Z hZ
  replace H := hZ.stableUnderSpecialization.image H
  wlog hX : ‚àÉ R, X = Spec R
  ¬∑ obtain ‚ü®R, g, hg‚ü© :=
      compactSpace_iff_exists.mp ((quasiCompact_over_affine_iff f).mp inferInstance)
    have inst : QuasiCompact (g ‚â´ f) := HasAffineProperty.iff_of_isAffine.mpr (by infer_instance)
    have := this _ (g ‚â´ f) (g.base ‚Åª¬π' Z) (hZ.preimage g.continuous)
    simp_rw [Scheme.comp_base, TopCat.comp_app, ‚Üê Set.image_image,
      Set.image_preimage_eq _ hg] at this
    exact this H ‚ü®_, rfl‚ü©
  obtain ‚ü®R, rfl‚ü© := hX
  obtain ‚ü®œÜ, rfl‚ü© := Spec.homEquiv.symm.surjective f
  exact PrimeSpectrum.isClosed_image_of_stableUnderSpecialization œÜ.hom Z hZ H

@[elab_as_elim]
theorem compact_open_induction_on {P : X.Opens ‚Üí Prop} (S : X.Opens)
    (hS : IsCompact S.1) (h‚ÇÅ : P ‚ä•)
    (h‚ÇÇ : ‚àÄ (S : X.Opens) (_ : IsCompact S.1) (U : X.affineOpens), P S ‚Üí P (S ‚äî U)) :
    P S := by
  classical
  obtain ‚ü®s, hs, hs'‚ü© := (isCompactOpen_iff_eq_finset_affine_union S.1).mp ‚ü®hS, S.2‚ü©
  replace hs' : S = iSup fun i : s => (i : X.Opens) := by ext1; simpa using hs'
  subst hs'
  apply @Set.Finite.induction_on _ _ _ hs
  ¬∑ convert h‚ÇÅ; rw [iSup_eq_bot]; rintro ‚ü®_, h‚ü©; exact h.elim
  ¬∑ intro x s _ hs h‚ÇÑ
    have : IsCompact (‚®Ü i : s, (i : X.Opens)).1 := by
      refine ((isCompactOpen_iff_eq_finset_affine_union _).mpr ?_).1; exact ‚ü®s, hs, by simp‚ü©
    convert h‚ÇÇ _ this x h‚ÇÑ
    rw [iSup_subtype, sup_comm]
    conv_rhs => rw [iSup_subtype]
    exact iSup_insert

theorem exists_pow_mul_eq_zero_of_res_basicOpen_eq_zero_of_isAffineOpen (X : Scheme)
    {U : X.Opens} (hU : IsAffineOpen U) (x f : Œì(X, U))
    (H : x |_ (X.basicOpen f) = 0) :
    ‚àÉ n : ‚Ñï, f ^ n * x = 0 := by
  rw [‚Üê map_zero (X.presheaf.map (homOfLE <| X.basicOpen_le f : X.basicOpen f ‚ü∂ U).op).hom] at H
  obtain ‚ü®n, e‚ü© := (hU.isLocalization_basicOpen f).exists_of_eq _ H
  exact ‚ü®n, by simpa [mul_comm x] using e‚ü©

/-- If `x : Œì(X, U)` is zero on `D(f)` for some `f : Œì(X, U)`, and `U` is quasi-compact, then
`f ^ n * x = 0` for some `n`. -/
theorem exists_pow_mul_eq_zero_of_res_basicOpen_eq_zero_of_isCompact (X : Scheme.{u})
    {U : X.Opens} (hU : IsCompact U.1) (x f : Œì(X, U))
    (H : x |_ (X.basicOpen f) = 0) :
    ‚àÉ n : ‚Ñï, f ^ n * x = 0 := by
  obtain ‚ü®s, hs, e‚ü© := (isCompactOpen_iff_eq_finset_affine_union U.1).mp ‚ü®hU, U.2‚ü©
  replace e : U = iSup fun i : s => (i : X.Opens) := by
    ext1; simpa using e
  have h‚ÇÅ (i : s) : i.1.1 ‚â§ U := by
    rw [e]
    exact le_iSup (fun (i : s) => (i : Opens (X.toPresheafedSpace))) _
  have H' := fun i : s =>
    exists_pow_mul_eq_zero_of_res_basicOpen_eq_zero_of_isAffineOpen X i.1.2
      (X.presheaf.map (homOfLE (h‚ÇÅ i)).op x) (X.presheaf.map (homOfLE (h‚ÇÅ i)).op f) ?_
  swap
  ¬∑ change (X.presheaf.map (homOfLE _).op) ((X.presheaf.map (homOfLE _).op).hom x) = 0
    have H : (X.presheaf.map (homOfLE _).op) x = 0 := H
    convert congr_arg (X.presheaf.map (homOfLE _).op).hom H
    ¬∑ simp only [‚Üê CommRingCat.comp_apply, ‚Üê Functor.map_comp]
      ¬∑ rfl
    ¬∑ rw [map_zero]
    ¬∑ simp only [Scheme.basicOpen_res, inf_le_right]
  choose n hn using H'
  haveI := hs.to_subtype
  cases nonempty_fintype s
  use Finset.univ.sup n
  suffices ‚àÄ i : s, X.presheaf.map (homOfLE (h‚ÇÅ i)).op (f ^ Finset.univ.sup n * x) = 0 by
    subst e
    apply TopCat.Sheaf.eq_of_locally_eq X.sheaf fun i : s => (i : X.Opens)
    intro i
    change _ = (X.sheaf.val.map _) 0
    rw [map_zero]
    apply this
  intro i
  replace hn :=
    congr_arg (fun x => X.presheaf.map (homOfLE (h‚ÇÅ i)).op (f ^ (Finset.univ.sup n - n i)) * x)
      (hn i)
  dsimp at hn
  simp only [‚Üê map_mul, ‚Üê map_pow] at hn
  rwa [mul_zero, ‚Üê mul_assoc, ‚Üê pow_add, tsub_add_cancel_of_le] at hn
  apply Finset.le_sup (Finset.mem_univ i)

/-- A section over a compact open of a scheme is nilpotent if and only if its associated
basic open is empty. -/
lemma Scheme.isNilpotent_iff_basicOpen_eq_bot_of_isCompact {X : Scheme.{u}}
    {U : X.Opens} (hU : IsCompact (U : Set X)) (f : Œì(X, U)) :
    IsNilpotent f ‚Üî X.basicOpen f = ‚ä• := by
  refine ‚ü®X.basicOpen_eq_bot_of_isNilpotent U f, fun hf ‚Ü¶ ?_‚ü©
  have h : (1 : Œì(X, U)) |_ (X.basicOpen f) = 0 := by
    have e : X.basicOpen f ‚â§ ‚ä• := by rw [hf]
    rw [‚Üê TopCat.Presheaf.restrict_restrict e bot_le]
    rw [Subsingleton.eq_zero (1 |_ ‚ä•)]
    change X.presheaf.map _ 0 = 0
    rw [map_zero]
  obtain ‚ü®n, hn‚ü© := exists_pow_mul_eq_zero_of_res_basicOpen_eq_zero_of_isCompact X hU 1 f h
  rw [mul_one] at hn
  use n

/-- A global section of a quasi-compact scheme is nilpotent if and only if its associated
basic open is empty. -/
lemma Scheme.isNilpotent_iff_basicOpen_eq_bot {X : Scheme.{u}}
    [CompactSpace X] (f : Œì(X, ‚ä§)) :
    IsNilpotent f ‚Üî X.basicOpen f = ‚ä• :=
  isNilpotent_iff_basicOpen_eq_bot_of_isCompact (U := ‚ä§) (CompactSpace.isCompact_univ) f

/-- The zero locus of a set of sections over a compact open of a scheme is `X` if and only if
`s` is contained in the nilradical of `Œì(X, U)`. -/
lemma Scheme.zeroLocus_eq_univ_iff_subset_nilradical_of_isCompact {X : Scheme.{u}} {U : X.Opens}
    (hU : IsCompact (U : Set X)) (s : Set Œì(X, U)) :
    X.zeroLocus s = Set.univ ‚Üî s ‚äÜ nilradical Œì(X, U) := by
  simp [Scheme.zeroLocus_def, ‚Üê Scheme.isNilpotent_iff_basicOpen_eq_bot_of_isCompact hU,
    ‚Üê mem_nilradical, Set.subset_def]

/-- The zero locus of a set of sections over a compact open of a scheme is `X` if and only if
`s` is contained in the nilradical of `Œì(X, U)`. -/
lemma Scheme.zeroLocus_eq_univ_iff_subset_nilradical {X : Scheme.{u}}
    [CompactSpace X] (s : Set Œì(X, ‚ä§)) :
    X.zeroLocus s = Set.univ ‚Üî s ‚äÜ nilradical Œì(X, ‚ä§) :=
  zeroLocus_eq_univ_iff_subset_nilradical_of_isCompact (U := ‚ä§) (CompactSpace.isCompact_univ) s

@[deprecated (since := "2025-04-05")]
alias Scheme.zeroLocus_eq_top_iff_subset_nilradical_of_isCompact :=
  Scheme.zeroLocus_eq_univ_iff_subset_nilradical_of_isCompact

@[deprecated (since := "2025-04-05")]
alias Scheme.zeroLocus_eq_top_iff_subset_nilradical :=
  Scheme.zeroLocus_eq_univ_iff_subset_nilradical

end AlgebraicGeometry
