/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.Constructors
import Mathlib.AlgebraicGeometry.Morphisms.QuasiCompact
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.Equalizer
import Mathlib.Topology.QuasiSeparated
import Mathlib.Topology.Sheaves.CommRingCat

/-!
# Quasi-separated morphisms

A morphism of schemes `f : X ‚ü∂ Y` is quasi-separated if the diagonal morphism `X ‚ü∂ X √ó[Y] X` is
quasi-compact.

A scheme is quasi-separated if the intersections of any two affine open sets is quasi-compact.
(`AlgebraicGeometry.quasiSeparatedSpace_iff_affine`)

We show that a morphism is quasi-separated if the preimage of every affine open is quasi-separated.

We also show that this property is local at the target,
and is stable under compositions and base-changes.

## Main result
- `AlgebraicGeometry.isLocalization_basicOpen_of_qcqs` (**Qcqs lemma**):
  If `U` is qcqs, then `Œì(X, D(f)) ‚âÉ Œì(X, U)_f` for every `f : Œì(X, U)`.

-/

noncomputable section

open CategoryTheory CategoryTheory.Limits Opposite TopologicalSpace

universe u

open scoped AlgebraicGeometry

namespace AlgebraicGeometry

variable {X Y Z : Scheme.{u}} (f : X ‚ü∂ Y)

/-- A morphism is `QuasiSeparated` if diagonal map is quasi-compact. -/
@[mk_iff]
class QuasiSeparated (f : X ‚ü∂ Y) : Prop where
  /-- A morphism is `QuasiSeparated` if diagonal map is quasi-compact. -/
  quasiCompact_diagonal : QuasiCompact (pullback.diagonal f) := by infer_instance

attribute [instance] QuasiSeparated.quasiCompact_diagonal

@[deprecated (since := "2025-10-15")]
alias QuasiSeparated.diagonalQuasiCompact := QuasiSeparated.quasiCompact_diagonal

theorem quasiSeparatedSpace_iff_forall_affineOpens {X : Scheme} :
    QuasiSeparatedSpace X ‚Üî ‚àÄ U V : X.affineOpens, IsCompact (U ‚à© V : Set X) := by
  rw [quasiSeparatedSpace_iff]
  constructor
  ¬∑ intro H U V; exact H U V U.1.2 U.2.isCompact V.1.2 V.2.isCompact
  ¬∑ intro H
    suffices
      ‚àÄ (U : X.Opens) (_ : IsCompact U.1) (V : X.Opens) (_ : IsCompact V.1),
        IsCompact (U ‚äì V).1
      by intro U V hU hU' hV hV'; exact this ‚ü®U, hU‚ü© hU' ‚ü®V, hV‚ü© hV'
    intro U hU V hV
    refine compact_open_induction_on V hV ?_ ?_
    ¬∑ simp
    ¬∑ intro S _ V hV
      change IsCompact (U.1 ‚à© (S.1 ‚à™ V.1))
      rw [Set.inter_union_distrib_left]
      apply hV.union
      clear hV
      refine compact_open_induction_on U hU ?_ ?_
      ¬∑ simp
      ¬∑ intro S _ W hW
        change IsCompact ((S.1 ‚à™ W.1) ‚à© V.1)
        rw [Set.union_inter_distrib_right]
        apply hW.union
        apply H

@[deprecated (since := "2025-10-15")]
alias quasiSeparatedSpace_iff_affine := quasiSeparatedSpace_iff_forall_affineOpens

theorem quasiCompact_affineProperty_iff_quasiSeparatedSpace [IsAffine Y] (f : X ‚ü∂ Y) :
    AffineTargetMorphismProperty.diagonal (fun X _ _ _ ‚Ü¶ CompactSpace X) f ‚Üî
      QuasiSeparatedSpace X := by
  delta AffineTargetMorphismProperty.diagonal
  rw [quasiSeparatedSpace_iff_forall_affineOpens]
  constructor
  ¬∑ intro H U V
    haveI : IsAffine _ := U.2
    haveI : IsAffine _ := V.2
    let g : pullback U.1.Œπ V.1.Œπ ‚ü∂ X := pullback.fst _ _ ‚â´ U.1.Œπ
    have e := g.isOpenEmbedding.isEmbedding.toHomeomorph
    rw [IsOpenImmersion.range_pullback_to_base_of_left] at e
    erw [Subtype.range_coe, Subtype.range_coe] at e
    rw [isCompact_iff_compactSpace]
    exact @Homeomorph.compactSpace _ _ _ _ (H _ _) e
  ¬∑ introv H h‚ÇÅ h‚ÇÇ
    let g : pullback f‚ÇÅ f‚ÇÇ ‚ü∂ X := pullback.fst _ _ ‚â´ f‚ÇÅ
    have e := g.isOpenEmbedding.isEmbedding.toHomeomorph
    rw [IsOpenImmersion.range_pullback_to_base_of_left] at e
    simp_rw [isCompact_iff_compactSpace] at H
    exact @Homeomorph.compactSpace _ _ _ _
        (H ‚ü®_, isAffineOpen_opensRange f‚ÇÅ‚ü© ‚ü®_, isAffineOpen_opensRange f‚ÇÇ‚ü©) e.symm

theorem quasiSeparated_eq_diagonal_is_quasiCompact :
    @QuasiSeparated = MorphismProperty.diagonal @QuasiCompact := by ext; exact quasiSeparated_iff _

instance : HasAffineProperty @QuasiSeparated (fun X _ _ _ ‚Ü¶ QuasiSeparatedSpace X) where
  __ := HasAffineProperty.copy
    quasiSeparated_eq_diagonal_is_quasiCompact.symm
    (by ext; exact quasiCompact_affineProperty_iff_quasiSeparatedSpace _)

instance (priority := 900) (f : X ‚ü∂ Y) [Mono f] :
    QuasiSeparated f where

instance quasiSeparated_isStableUnderComposition :
    MorphismProperty.IsStableUnderComposition @QuasiSeparated :=
  quasiSeparated_eq_diagonal_is_quasiCompact.symm ‚ñ∏ inferInstance

instance : MorphismProperty.IsMultiplicative @QuasiSeparated where
  id_mem _ := inferInstance

instance quasiSeparated_isStableUnderBaseChange :
    MorphismProperty.IsStableUnderBaseChange @QuasiSeparated :=
  quasiSeparated_eq_diagonal_is_quasiCompact.symm ‚ñ∏ inferInstance

instance quasiSeparated_comp (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [QuasiSeparated f]
    [QuasiSeparated g] : QuasiSeparated (f ‚â´ g) :=
  MorphismProperty.comp_mem _ f g inferInstance inferInstance

theorem quasiSeparatedSpace_iff_quasiSeparated (X : Scheme) :
    QuasiSeparatedSpace X ‚Üî QuasiSeparated (terminal.from X) :=
  (HasAffineProperty.iff_of_isAffine (P := @QuasiSeparated)).symm

instance {X Y S : Scheme} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [QuasiSeparated g] :
    QuasiSeparated (pullback.fst f g) :=
  MorphismProperty.pullback_fst f g inferInstance

instance {X Y S : Scheme} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [QuasiSeparated f] :
    QuasiSeparated (pullback.snd f g) :=
  MorphismProperty.pullback_snd f g inferInstance

instance (f : X ‚ü∂ Y) (V : Y.Opens) [QuasiSeparated f] : QuasiSeparated (f ‚à£_ V) :=
  IsZariskiLocalAtTarget.restrict ‚Äπ_‚Ä∫ V

instance (f : X ‚ü∂ Y) (U : X.Opens) (V : Y.Opens) (e) [QuasiSeparated f] :
    QuasiSeparated (f.resLE V U e) := by
  delta Scheme.Hom.resLE; infer_instance

theorem quasiSeparatedSpace_of_quasiSeparated (f : X ‚ü∂ Y)
    [hY : QuasiSeparatedSpace Y] [QuasiSeparated f] : QuasiSeparatedSpace X := by
  rw [quasiSeparatedSpace_iff_quasiSeparated] at hY ‚ä¢
  rw [‚Üê terminalIsTerminal.hom_ext (f ‚â´ terminal.from Y) (terminal.from X)]
  infer_instance

instance quasiSeparatedSpace_of_isAffine (X : Scheme) [IsAffine X] : QuasiSeparatedSpace X :=
  (quasiSeparatedSpace_congr X.isoSpec.hom.homeomorph).2 PrimeSpectrum.instQuasiSeparatedSpace

theorem IsAffineOpen.isQuasiSeparated {U : X.Opens} (hU : IsAffineOpen U) :
    IsQuasiSeparated (U : Set X) := by
  rw [isQuasiSeparated_iff_quasiSeparatedSpace]
  exacts [@AlgebraicGeometry.quasiSeparatedSpace_of_isAffine _ hU, U.isOpen]

instance [QuasiSeparatedSpace X] : QuasiSeparated X.toSpecŒì :=
  HasAffineProperty.iff_of_isAffine.mpr ‚Äπ_‚Ä∫

theorem Scheme.quasiSeparatedSpace_of_isOpenCover
    {I : Type*} (U : I ‚Üí X.Opens) (hU : IsOpenCover U)
    (hU‚ÇÅ : ‚àÄ i, IsAffineOpen (U i)) (hU‚ÇÇ : ‚àÄ i j, IsCompact (X := X) (U i ‚à© U j)) :
    QuasiSeparatedSpace X := by
  letI := HasAffineProperty.isLocal_affineProperty @QuasiCompact
  rw [‚Üê quasiCompact_affineProperty_iff_quasiSeparatedSpace X.toSpecŒì]
  have : ‚àÄ i, IsAffine ((X.openCoverOfIsOpenCover U hU).X i) := hU‚ÇÅ
  refine AffineTargetMorphismProperty.diagonal_of_openCover_source _
    (Scheme.openCoverOfIsOpenCover _ _ hU) fun i j ‚Ü¶ ?_
  rw [‚Üê isCompact_univ_iff, (pullback.fst ((X.openCoverOfIsOpenCover U hU).f i)
    ((X.openCoverOfIsOpenCover U hU).f j) ‚â´
    (X.openCoverOfIsOpenCover U hU).f i).isOpenEmbedding.isCompact_iff, Set.image_univ,
    IsOpenImmersion.range_pullback_to_base_of_left]
  simpa using hU‚ÇÇ i j

lemma quasiSeparatedSpace_iff_quasiCompact_prod_lift :
    QuasiSeparatedSpace X ‚Üî QuasiCompact (prod.lift (ùüô X) (ùüô X)) := by
  rw [‚Üê MorphismProperty.cancel_right_of_respectsIso @QuasiCompact _ (prodIsoPullback X X).hom,
    ‚Üê HasAffineProperty.iff_of_isAffine (f := terminal.from X) (P := @QuasiSeparated),
    quasiSeparated_iff]
  congr!
  ext : 1 <;> simp

instance [QuasiSeparatedSpace X] : QuasiCompact (prod.lift (ùüô X) (ùüô X)) := by
  rwa [‚Üê quasiSeparatedSpace_iff_quasiCompact_prod_lift]

instance [QuasiSeparatedSpace Y] (f g : X ‚ü∂ Y) : QuasiCompact (equalizer.Œπ f g) :=
  MorphismProperty.of_isPullback (P := @QuasiCompact)
    (isPullback_equalizer_prod f g).flip inferInstance

instance [CompactSpace X] [QuasiSeparatedSpace Y] (f g : X ‚ü∂ Y) :
    CompactSpace (equalizer f g).carrier := by
  constructor
  simpa using QuasiCompact.isCompact_preimage (f := equalizer.Œπ f g) _ isOpen_univ isCompact_univ

theorem QuasiSeparated.of_comp (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [QuasiSeparated (f ‚â´ g)] :
    QuasiSeparated f := by
  let ùí∞ := (Z.affineCover.pullback‚ÇÅ g).bind fun x => Scheme.affineCover _
  have (i : _) : IsAffine (ùí∞.X i) := by dsimp [ùí∞]; infer_instance
  apply HasAffineProperty.of_openCover
    ((Z.affineCover.pullback‚ÇÅ g).bind fun x => Scheme.affineCover _)
  rintro ‚ü®i, j‚ü©; dsimp at i j
  refine @quasiSeparatedSpace_of_quasiSeparated _ _ ?_
    (HasAffineProperty.of_isPullback (.of_hasPullback _ (Z.affineCover.f i)) ‚Äπ_‚Ä∫) ?_
  ¬∑ exact pullback.map _ _ _ _ (ùüô _) _ _ (by simp) (Category.comp_id _) ‚â´
      (pullbackRightPullbackFstIso g (Z.affineCover.f i) f).hom
  ¬∑ exact inferInstance

instance (priority := low) QuasiSeparated.of_quasiSeparatedSpace
    (f : X ‚ü∂ Y) [QuasiSeparatedSpace X] : QuasiSeparated f :=
  have : QuasiSeparated (f ‚â´ Y.toSpecŒì) :=
    (HasAffineProperty.iff_of_isAffine (P := @QuasiSeparated)).mpr ‚Äπ_‚Ä∫
  .of_comp f Y.toSpecŒì

theorem quasiSeparated_iff_quasiSeparatedSpace (f : X ‚ü∂ Y) [QuasiSeparatedSpace Y] :
    QuasiSeparated f ‚Üî QuasiSeparatedSpace X :=
  ‚ü®fun _ ‚Ü¶ quasiSeparatedSpace_of_quasiSeparated f, fun _ ‚Ü¶ inferInstance‚ü©

@[deprecated (since := "2025-10-15")]
alias quasiSeparated_over_affine_iff := quasiSeparated_iff_quasiSeparatedSpace

instance : MorphismProperty.HasOfPostcompProperty @QuasiSeparated ‚ä§ where
  of_postcomp f g _ _ := .of_comp f g

instance : MorphismProperty.HasOfPostcompProperty @QuasiCompact @QuasiSeparated :=
  MorphismProperty.hasOfPostcompProperty_iff_le_diagonal.mpr
    (by rw [quasiSeparated_eq_diagonal_is_quasiCompact])

lemma QuasiCompact.of_comp (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [QuasiCompact (f ‚â´ g)] [QuasiSeparated g] :
    QuasiCompact f :=
  MorphismProperty.of_postcomp _ _ g ‚Äπ_‚Ä∫ ‚Äπ_‚Ä∫

instance (priority := low) quasiCompact_of_compactSpace {X Y : Scheme} (f : X ‚ü∂ Y)
    [CompactSpace X] [QuasiSeparatedSpace Y] : QuasiCompact f :=
  have : QuasiCompact (f ‚â´ Y.toSpecŒì) := HasAffineProperty.iff_of_isAffine.mpr ‚Äπ_‚Ä∫
  .of_comp f Y.toSpecŒì

theorem quasiCompact_iff_compactSpace (f : X ‚ü∂ Y) [QuasiSeparatedSpace Y] [CompactSpace Y] :
    QuasiCompact f ‚Üî CompactSpace X :=
  ‚ü®fun _ ‚Ü¶ QuasiCompact.compactSpace_of_compactSpace f, fun _ ‚Ü¶ inferInstance‚ü©

@[deprecated (since := "2025-10-15")]
alias quasiCompact_over_affine_iff := quasiCompact_iff_compactSpace

theorem exists_eq_pow_mul_of_isAffineOpen (X : Scheme) (U : X.Opens) (hU : IsAffineOpen U)
    (f : Œì(X, U)) (x : Œì(X, X.basicOpen f)) :
    ‚àÉ (n : ‚Ñï) (y : Œì(X, U)), y |_ X.basicOpen f = (f |_ X.basicOpen f) ^ n * x := by
  have := (hU.isLocalization_basicOpen f).2
  obtain ‚ü®‚ü®y, _, n, rfl‚ü©, d‚ü© := this x
  use n, y
  simpa [mul_comm x] using d.symm

theorem exists_eq_pow_mul_of_is_compact_of_quasi_separated_space_aux_aux {X : TopCat}
    (F : X.Presheaf CommRingCat) {U‚ÇÅ U‚ÇÇ U‚ÇÉ U‚ÇÑ U‚ÇÖ U‚ÇÜ U‚Çá : Opens X} {n‚ÇÅ n‚ÇÇ : ‚Ñï}
    {y‚ÇÅ : F.obj (op U‚ÇÅ)} {y‚ÇÇ : F.obj (op U‚ÇÇ)} {f : F.obj (op <| U‚ÇÅ ‚äî U‚ÇÇ)}
    {x : F.obj (op U‚ÇÉ)} (h‚ÇÑ‚ÇÅ : U‚ÇÑ ‚â§ U‚ÇÅ) (h‚ÇÑ‚ÇÇ : U‚ÇÑ ‚â§ U‚ÇÇ) (h‚ÇÖ‚ÇÅ : U‚ÇÖ ‚â§ U‚ÇÅ) (h‚ÇÖ‚ÇÉ : U‚ÇÖ ‚â§ U‚ÇÉ)
    (h‚ÇÜ‚ÇÇ : U‚ÇÜ ‚â§ U‚ÇÇ) (h‚ÇÜ‚ÇÉ : U‚ÇÜ ‚â§ U‚ÇÉ) (h‚Çá‚ÇÑ : U‚Çá ‚â§ U‚ÇÑ) (h‚Çá‚ÇÖ : U‚Çá ‚â§ U‚ÇÖ) (h‚Çá‚ÇÜ : U‚Çá ‚â§ U‚ÇÜ)
    (e‚ÇÅ : y‚ÇÅ |_ U‚ÇÖ = (f |_ U‚ÇÅ |_ U‚ÇÖ) ^ n‚ÇÅ * x |_ U‚ÇÖ)
    (e‚ÇÇ : y‚ÇÇ |_ U‚ÇÜ = (f |_ U‚ÇÇ |_ U‚ÇÜ) ^ n‚ÇÇ * x |_ U‚ÇÜ) :
    (((f |_ U‚ÇÅ) ^ n‚ÇÇ * y‚ÇÅ) |_ U‚ÇÑ) |_ U‚Çá = (((f |_ U‚ÇÇ) ^ n‚ÇÅ * y‚ÇÇ) |_ U‚ÇÑ) |_ U‚Çá := by
  apply_fun (fun x : F.obj (op U‚ÇÖ) ‚Ü¶ x |_ U‚Çá) at e‚ÇÅ
  apply_fun (fun x : F.obj (op U‚ÇÜ) ‚Ü¶ x |_ U‚Çá) at e‚ÇÇ
  dsimp only [TopCat.Presheaf.restrictOpenCommRingCat_apply] at e‚ÇÅ e‚ÇÇ ‚ä¢
  simp only [map_mul, map_pow, ‚Üê op_comp, ‚Üê F.map_comp, homOfLE_comp, ‚Üê CommRingCat.comp_apply]
    at e‚ÇÅ e‚ÇÇ ‚ä¢
  rw [e‚ÇÅ, e‚ÇÇ, mul_left_comm]

theorem exists_eq_pow_mul_of_is_compact_of_quasi_separated_space_aux (X : Scheme)
    (S : X.affineOpens) (U‚ÇÅ U‚ÇÇ : X.Opens) {n‚ÇÅ n‚ÇÇ : ‚Ñï} {y‚ÇÅ : Œì(X, U‚ÇÅ)}
    {y‚ÇÇ : Œì(X, U‚ÇÇ)} {f : Œì(X, U‚ÇÅ ‚äî U‚ÇÇ)}
    {x : Œì(X, X.basicOpen f)} (h‚ÇÅ : S.1 ‚â§ U‚ÇÅ) (h‚ÇÇ : S.1 ‚â§ U‚ÇÇ)
    (e‚ÇÅ : y‚ÇÅ |_ X.basicOpen (f |_ U‚ÇÅ) =
      ((f |_ U‚ÇÅ |_ X.basicOpen _) ^ n‚ÇÅ) * x |_ X.basicOpen _)
    (e‚ÇÇ : y‚ÇÇ |_ X.basicOpen (f |_ U‚ÇÇ) =
      ((f |_ U‚ÇÇ |_ X.basicOpen _) ^ n‚ÇÇ) * x |_ X.basicOpen _) :
    ‚àÉ n : ‚Ñï, ‚àÄ m, n ‚â§ m ‚Üí
      ((f |_ U‚ÇÅ) ^ (m + n‚ÇÇ) * y‚ÇÅ) |_ S.1 = ((f |_ U‚ÇÇ) ^ (m + n‚ÇÅ) * y‚ÇÇ) |_ S.1 := by
  obtain ‚ü®‚ü®_, n, rfl‚ü©, e‚ü© :=
    (@IsLocalization.eq_iff_exists _ _ _ _ _ _
      (S.2.isLocalization_basicOpen (f |_ S.1))
        (((f |_ U‚ÇÅ) ^ n‚ÇÇ * y‚ÇÅ) |_ S.1)
        (((f |_ U‚ÇÇ) ^ n‚ÇÅ * y‚ÇÇ) |_ S.1)).mp <| by
    apply exists_eq_pow_mul_of_is_compact_of_quasi_separated_space_aux_aux (e‚ÇÅ := e‚ÇÅ) (e‚ÇÇ := e‚ÇÇ)
    ¬∑ change X.basicOpen _ ‚â§ _
      simp only [TopCat.Presheaf.restrictOpenCommRingCat_apply, Scheme.basicOpen_res]
      exact inf_le_inf h‚ÇÅ le_rfl
    ¬∑ change X.basicOpen _ ‚â§ _
      simp only [TopCat.Presheaf.restrictOpenCommRingCat_apply, Scheme.basicOpen_res]
      exact inf_le_inf h‚ÇÇ le_rfl
  use n
  intro m hm
  rw [‚Üê tsub_add_cancel_of_le hm]
  simp only [TopCat.Presheaf.restrictOpenCommRingCat_apply,
    pow_add, map_pow, map_mul, mul_assoc, ‚Üê Functor.map_comp, ‚Üê op_comp, homOfLE_comp,
    ‚Üê CommRingCat.comp_apply] at e ‚ä¢
  rw [e]

theorem exists_eq_pow_mul_of_isCompact_of_isQuasiSeparated (X : Scheme.{u}) (U : X.Opens)
    (hU : IsCompact U.1) (hU' : IsQuasiSeparated U.1) (f : Œì(X, U)) (x : Œì(X, X.basicOpen f)) :
    ‚àÉ (n : ‚Ñï) (y : Œì(X, U)), y |_ X.basicOpen f = (f |_ X.basicOpen f) ^ n * x := by
  dsimp only [TopCat.Presheaf.restrictOpenCommRingCat_apply]
  revert hU' f x
  refine compact_open_induction_on U hU ?_ ?_
  ¬∑ intro _ f x
    use 0, f
    refine @Subsingleton.elim _
      (CommRingCat.subsingleton_of_isTerminal (X.sheaf.isTerminalOfEqEmpty ?_)) _ _
    rw [eq_bot_iff]
    exact X.basicOpen_le f
  ¬∑ -- Given `f : ùí™(S ‚à™ U), x : ùí™(X_f)`, we need to show that `f ^ n * x` is the restriction of
    -- some `y : ùí™(S ‚à™ U)` for some `n : ‚Ñï`.
    intro S hS U hU hSU f x
    -- We know that such `y‚ÇÅ, n‚ÇÅ` exists on `S` by the induction hypothesis.
    obtain ‚ü®n‚ÇÅ, y‚ÇÅ, hy‚ÇÅ‚ü© :=
      hU (hSU.of_subset Set.subset_union_left) (X.presheaf.map (homOfLE le_sup_left).op f)
        (X.presheaf.map (homOfLE _).op x)
    -- ¬∑ rw [X.basicOpen_res]; exact inf_le_right
    -- We know that such `y‚ÇÇ, n‚ÇÇ` exists on `U` since `U` is affine.
    obtain ‚ü®n‚ÇÇ, y‚ÇÇ, hy‚ÇÇ‚ü© :=
      exists_eq_pow_mul_of_isAffineOpen X _ U.2 (X.presheaf.map (homOfLE le_sup_right).op f)
        (X.presheaf.map (homOfLE _).op x)
    dsimp only [TopCat.Presheaf.restrictOpenCommRingCat_apply] at hy‚ÇÇ
    -- swap; ¬∑ rw [X.basicOpen_res]; exact inf_le_right
    -- Since `S ‚à™ U` is quasi-separated, `S ‚à© U` can be covered by finite affine opens.
    obtain ‚ü®s, hs', hs‚ü© :=
      isCompact_and_isOpen_iff_finite_and_eq_biUnion_affineOpens.mp
        ‚ü®hSU _ _ Set.subset_union_left S.2 hS Set.subset_union_right U.1.2
            U.2.isCompact,
          (S ‚äì U.1).2‚ü©
    haveI := hs'.to_subtype
    cases nonempty_fintype s
    replace hs : S ‚äì U.1 = iSup fun i : s => (i : X.Opens) := by ext1; simpa using hs
    have hs‚ÇÅ (i : s) : i.1.1 ‚â§ S := by
      refine le_trans ?_ (inf_le_left (b := U.1))
      rw [hs]
      exact le_iSup (fun (i : s) => (i : X.Opens)) i
    have hs‚ÇÇ (i : s) : i.1.1 ‚â§ U.1 := by
      refine le_trans ?_ (inf_le_right (a := S))
      rw [hs]
      exact le_iSup (fun (i : s) => (i : X.Opens)) i
    -- On each affine open in the intersection, we have `f ^ (n + n‚ÇÇ) * y‚ÇÅ = f ^ (n + n‚ÇÅ) * y‚ÇÇ`
    -- for some `n` since `f ^ n‚ÇÇ * y‚ÇÅ = f ^ (n‚ÇÅ + n‚ÇÇ) * x = f ^ n‚ÇÅ * y‚ÇÇ` on `X_f`.
    have := fun i ‚Ü¶ exists_eq_pow_mul_of_is_compact_of_quasi_separated_space_aux
      X i.1 S U (hs‚ÇÅ i) (hs‚ÇÇ i) hy‚ÇÅ hy‚ÇÇ
    choose n hn using this
    -- We can thus choose a big enough `n` such that `f ^ (n + n‚ÇÇ) * y‚ÇÅ = f ^ (n + n‚ÇÅ) * y‚ÇÇ`
    -- on `S ‚à© U`.
    have :
      X.presheaf.map (homOfLE <| inf_le_left).op
          (X.presheaf.map (homOfLE le_sup_left).op f ^ (Finset.univ.sup n + n‚ÇÇ) * y‚ÇÅ) =
        X.presheaf.map (homOfLE <| inf_le_right).op
          (X.presheaf.map (homOfLE le_sup_right).op f ^ (Finset.univ.sup n + n‚ÇÅ) * y‚ÇÇ) := by
      fapply X.sheaf.eq_of_locally_eq' fun i : s => i.1.1
      ¬∑ refine fun i => homOfLE ?_; rw [hs]
        exact le_iSup (fun (i : s) => (i : X.Opens)) i
      ¬∑ exact le_of_eq hs
      ¬∑ intro i
        -- This unfolds `X.sheaf`
        change (X.presheaf.map _) _ = (X.presheaf.map _) _
        simp only [‚Üê CommRingCat.comp_apply, ‚Üê Functor.map_comp, ‚Üê op_comp]
        apply hn
        exact Finset.le_sup (Finset.mem_univ _)
    use Finset.univ.sup n + n‚ÇÅ + n‚ÇÇ
    -- By the sheaf condition, since `f ^ (n + n‚ÇÇ) * y‚ÇÅ = f ^ (n + n‚ÇÅ) * y‚ÇÇ`, it can be glued into
    -- the desired section on `S ‚à™ U`.
    use (X.sheaf.objSupIsoProdEqLocus S U.1).inv ‚ü®‚ü®_ * _, _ * _‚ü©, this‚ü©
    refine (X.sheaf.objSupIsoProdEqLocus_inv_eq_iff _ _ _ (X.basicOpen_res _
      (homOfLE le_sup_left).op) (X.basicOpen_res _ (homOfLE le_sup_right).op)).mpr ‚ü®?_, ?_‚ü©
    ¬∑ -- This unfolds `X.sheaf`
      change (X.presheaf.map _) _ = (X.presheaf.map _) _
      rw [add_assoc, add_comm n‚ÇÅ]
      simp only [pow_add, map_pow, map_mul, hy‚ÇÅ, ‚Üê CommRingCat.comp_apply, ‚Üê mul_assoc,
        ‚Üê Functor.map_comp, ‚Üê op_comp, homOfLE_comp]
    ¬∑ -- This unfolds `X.sheaf`
      change (X.presheaf.map _) _ = (X.presheaf.map _) _
      simp only [pow_add, map_pow, map_mul, hy‚ÇÇ, ‚Üê CommRingCat.comp_apply, ‚Üê mul_assoc,
        ‚Üê Functor.map_comp, ‚Üê op_comp, homOfLE_comp]

/-- If `U` is qcqs, then `Œì(X, D(f)) ‚âÉ Œì(X, U)_f` for every `f : Œì(X, U)`.
This is known as the **Qcqs lemma** in [R. Vakil, *The rising sea*][RisingSea]. -/
theorem isLocalization_basicOpen_of_qcqs {X : Scheme} {U : X.Opens} (hU : IsCompact U.1)
    (hU' : IsQuasiSeparated U.1) (f : Œì(X, U)) :
    IsLocalization.Away f (Œì(X, X.basicOpen f)) := by
  constructor
  ¬∑ rintro ‚ü®_, n, rfl‚ü©
    simp only [map_pow, RingHom.algebraMap_toAlgebra]
    exact IsUnit.pow _ (RingedSpace.isUnit_res_basicOpen _ f)
  ¬∑ intro z
    obtain ‚ü®n, y, e‚ü© := exists_eq_pow_mul_of_isCompact_of_isQuasiSeparated X U hU hU' f z
    refine ‚ü®‚ü®y, _, n, rfl‚ü©, ?_‚ü©
    simpa only [map_pow, Subtype.coe_mk, RingHom.algebraMap_toAlgebra, mul_comm z] using e.symm
  ¬∑ intro x y
    rw [‚Üê sub_eq_zero, ‚Üê map_sub, RingHom.algebraMap_toAlgebra]
    simp_rw [‚Üê @sub_eq_zero _ _ (_ * x) (_ * y), ‚Üê mul_sub]
    generalize x - y = z
    intro H
    obtain ‚ü®n, e‚ü© := exists_pow_mul_eq_zero_of_res_basicOpen_eq_zero_of_isCompact X hU _ _ H
    refine ‚ü®‚ü®_, n, rfl‚ü©, ?_‚ü©
    simpa [mul_comm z] using e

lemma exists_of_res_eq_of_qcqs {X : Scheme.{u}} {U : TopologicalSpace.Opens X}
    (hU : IsCompact U.carrier) (hU' : IsQuasiSeparated U.carrier)
    {f g s : Œì(X, U)} (hfg : f |_ X.basicOpen s = g |_ X.basicOpen s) :
    ‚àÉ n, s ^ n * f = s ^ n * g := by
  obtain ‚ü®n, hc‚ü© := (isLocalization_basicOpen_of_qcqs hU hU' s).exists_of_eq s hfg
  use n

lemma exists_of_res_eq_of_qcqs_of_top {X : Scheme.{u}} [CompactSpace X] [QuasiSeparatedSpace X]
    {f g s : Œì(X, ‚ä§)} (hfg : f |_ X.basicOpen s = g |_ X.basicOpen s) :
    ‚àÉ n, s ^ n * f = s ^ n * g :=
  exists_of_res_eq_of_qcqs (U := ‚ä§) CompactSpace.isCompact_univ isQuasiSeparated_univ hfg

lemma exists_of_res_zero_of_qcqs {X : Scheme.{u}} {U : TopologicalSpace.Opens X}
    (hU : IsCompact U.carrier) (hU' : IsQuasiSeparated U.carrier)
    {f s : Œì(X, U)} (hf : f |_ X.basicOpen s = 0) :
    ‚àÉ n, s ^ n * f = 0 := by
  suffices h : ‚àÉ n, s ^ n * f = s ^ n * 0 by
    simpa using h
  apply exists_of_res_eq_of_qcqs hU hU'
  simpa

lemma exists_of_res_zero_of_qcqs_of_top {X : Scheme} [CompactSpace X] [QuasiSeparatedSpace X]
    {f s : Œì(X, ‚ä§)} (hf : f |_ X.basicOpen s = 0) :
    ‚àÉ n, s ^ n * f = 0 :=
  exists_of_res_zero_of_qcqs (U := ‚ä§) CompactSpace.isCompact_univ isQuasiSeparated_univ hf

/-- If `U` is qcqs, then `Œì(X, D(f)) ‚âÉ Œì(X, U)_f` for every `f : Œì(X, U)`.
This is known as the **Qcqs lemma** in [R. Vakil, *The rising sea*][RisingSea]. -/
instance isIso_ŒìSpec_adjunction_unit_app_basicOpen
    [CompactSpace X] [QuasiSeparatedSpace X] (f : Œì(X, ‚ä§)) :
    IsIso (X.toSpecŒì.app (PrimeSpectrum.basicOpen f)) := by
  refine @IsIso.of_isIso_comp_right _ _ _ _ _ _ (X.presheaf.map
    (eqToHom (Scheme.toSpecŒì_preimage_basicOpen _ _).symm).op) _ ?_
  rw [ConcreteCategory.isIso_iff_bijective]
  apply (config := { allowSynthFailures := true }) IsLocalization.bijective
  ¬∑ exact StructureSheaf.IsLocalization.to_basicOpen _ _
  ¬∑ refine isLocalization_basicOpen_of_qcqs ?_ ?_ _
    ¬∑ exact isCompact_univ
    ¬∑ exact isQuasiSeparated_univ
  ¬∑ simp [RingHom.algebraMap_toAlgebra, ‚Üê CommRingCat.hom_comp, RingHom.algebraMap_toAlgebra,
      ‚Üê Functor.map_comp]

end AlgebraicGeometry
