/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.QuasiCompact
import Mathlib.Topology.QuasiSeparated

#align_import algebraic_geometry.morphisms.quasi_separated from "leanprover-community/mathlib"@"1a51edf13debfcbe223fa06b1cb353b9ed9751cc"

/-!
# Quasi-separated morphisms

A morphism of schemes `f : X ‚ü∂ Y` is quasi-separated if the diagonal morphism `X ‚ü∂ X √ó[Y] X` is
quasi-compact.

A scheme is quasi-separated if the intersections of any two affine open sets is quasi-compact.
(`AlgebraicGeometry.quasiSeparatedSpace_iff_affine`)

We show that a morphism is quasi-separated if the preimage of every affine open is quasi-separated.

We also show that this property is local at the target,
and is stable under compositions and base-changes.

## Main result
- `AlgebraicGeometry.is_localization_basicOpen_of_qcqs` (**Qcqs lemma**):
  If `U` is qcqs, then `Œì(X, D(f)) ‚âÉ Œì(X, U)_f` for every `f : Œì(X, U)`.

-/


noncomputable section

open CategoryTheory CategoryTheory.Limits Opposite TopologicalSpace

universe u

open scoped AlgebraicGeometry

namespace AlgebraicGeometry

variable {X Y : Scheme.{u}} (f : X ‚ü∂ Y)

/-- A morphism is `QuasiSeparated` if diagonal map is quasi-compact. -/
@[mk_iff]
class QuasiSeparated (f : X ‚ü∂ Y) : Prop where
  /-- A morphism is `QuasiSeparated` if diagonal map is quasi-compact. -/
  diagonalQuasiCompact : QuasiCompact (pullback.diagonal f) := by infer_instance
#align algebraic_geometry.quasi_separated AlgebraicGeometry.QuasiSeparated

/-- The `AffineTargetMorphismProperty` corresponding to `QuasiSeparated`, asserting that the
domain is a quasi-separated scheme. -/
def QuasiSeparated.affineProperty : AffineTargetMorphismProperty := fun X _ _ _ =>
  QuasiSeparatedSpace X.carrier
#align algebraic_geometry.quasi_separated.affine_property AlgebraicGeometry.QuasiSeparated.affineProperty

theorem quasiSeparatedSpace_iff_affine (X : Scheme) :
    QuasiSeparatedSpace X.carrier ‚Üî ‚àÄ U V : X.affineOpens, IsCompact (U ‚à© V : Set X.carrier) := by
  rw [quasiSeparatedSpace_iff]
  constructor
  ¬∑ intro H U V; exact H U V U.1.2 U.2.isCompact V.1.2 V.2.isCompact
  ¬∑ intro H
    suffices
      ‚àÄ (U : Opens X.carrier) (_ : IsCompact U.1) (V : Opens X.carrier) (_ : IsCompact V.1),
        IsCompact (U ‚äì V).1
      by intro U V hU hU' hV hV'; exact this ‚ü®U, hU‚ü© hU' ‚ü®V, hV‚ü© hV'
    intro U hU V hV
    -- Porting note: it complains "unable to find motive", but telling Lean that motive is
    -- underscore is actually sufficient, weird
    apply compact_open_induction_on (P := _) V hV
    ¬∑ simp
    ¬∑ intro S _ V hV
      change IsCompact (U.1 ‚à© (S.1 ‚à™ V.1))
      rw [Set.inter_union_distrib_left]
      apply hV.union
      clear hV
      apply compact_open_induction_on (P := _) U hU
      ¬∑ simp
      ¬∑ intro S _ W hW
        change IsCompact ((S.1 ‚à™ W.1) ‚à© V.1)
        rw [Set.union_inter_distrib_right]
        apply hW.union
        apply H
#align algebraic_geometry.quasi_separated_space_iff_affine AlgebraicGeometry.quasiSeparatedSpace_iff_affine

theorem quasi_compact_affineProperty_iff_quasiSeparatedSpace {X Y : Scheme} [IsAffine Y]
    (f : X ‚ü∂ Y) : QuasiCompact.affineProperty.diagonal f ‚Üî QuasiSeparatedSpace X.carrier := by
  delta AffineTargetMorphismProperty.diagonal
  rw [quasiSeparatedSpace_iff_affine]
  constructor
  ¬∑ intro H U V
    haveI : IsAffine _ := U.2
    haveI : IsAffine _ := V.2
    let g : pullback (X.ofRestrict U.1.openEmbedding) (X.ofRestrict V.1.openEmbedding) ‚ü∂ X :=
      pullback.fst ‚â´ X.ofRestrict _
    -- Porting note: `inferInstance` does not work here
    have : IsOpenImmersion g := PresheafedSpace.IsOpenImmersion.comp _ _
    have e := Homeomorph.ofEmbedding _ this.base_open.toEmbedding
    rw [IsOpenImmersion.range_pullback_to_base_of_left] at e
    erw [Subtype.range_coe, Subtype.range_coe] at e
    rw [isCompact_iff_compactSpace]
    exact @Homeomorph.compactSpace _ _ _ _ (H _ _) e
  ¬∑ introv H h‚ÇÅ h‚ÇÇ
    let g : pullback f‚ÇÅ f‚ÇÇ ‚ü∂ X := pullback.fst ‚â´ f‚ÇÅ
    -- Porting note: `inferInstance` does not work here
    have : IsOpenImmersion g := PresheafedSpace.IsOpenImmersion.comp _ _
    have e := Homeomorph.ofEmbedding _ this.base_open.toEmbedding
    rw [IsOpenImmersion.range_pullback_to_base_of_left] at e
    simp_rw [isCompact_iff_compactSpace] at H
    exact
      @Homeomorph.compactSpace _ _ _ _
        (H ‚ü®‚ü®_, h‚ÇÅ.base_open.isOpen_range‚ü©, isAffineOpen_opensRange _‚ü©
          ‚ü®‚ü®_, h‚ÇÇ.base_open.isOpen_range‚ü©, isAffineOpen_opensRange _‚ü©)
        e.symm
#align algebraic_geometry.quasi_compact_affine_property_iff_quasi_separated_space AlgebraicGeometry.quasi_compact_affineProperty_iff_quasiSeparatedSpace

theorem quasiSeparated_eq_diagonal_is_quasiCompact :
    @QuasiSeparated = MorphismProperty.diagonal @QuasiCompact := by ext; exact quasiSeparated_iff _
#align algebraic_geometry.quasi_separated_eq_diagonal_is_quasi_compact AlgebraicGeometry.quasiSeparated_eq_diagonal_is_quasiCompact

theorem quasi_compact_affineProperty_diagonal_eq :
    QuasiCompact.affineProperty.diagonal = QuasiSeparated.affineProperty := by
  funext; rw [quasi_compact_affineProperty_iff_quasiSeparatedSpace]; rfl
#align algebraic_geometry.quasi_compact_affine_property_diagonal_eq AlgebraicGeometry.quasi_compact_affineProperty_diagonal_eq

theorem quasiSeparated_eq_affineProperty_diagonal :
    @QuasiSeparated = targetAffineLocally QuasiCompact.affineProperty.diagonal := by
  rw [quasiSeparated_eq_diagonal_is_quasiCompact, quasiCompact_eq_affineProperty]
  exact
    diagonal_targetAffineLocally_eq_targetAffineLocally _ QuasiCompact.affineProperty_isLocal
#align algebraic_geometry.quasi_separated_eq_affine_property_diagonal AlgebraicGeometry.quasiSeparated_eq_affineProperty_diagonal

theorem quasiSeparated_eq_affineProperty :
    @QuasiSeparated = targetAffineLocally QuasiSeparated.affineProperty := by
  rw [quasiSeparated_eq_affineProperty_diagonal, quasi_compact_affineProperty_diagonal_eq]
#align algebraic_geometry.quasi_separated_eq_affine_property AlgebraicGeometry.quasiSeparated_eq_affineProperty

theorem QuasiSeparated.affineProperty_isLocal : QuasiSeparated.affineProperty.IsLocal :=
  quasi_compact_affineProperty_diagonal_eq ‚ñ∏ QuasiCompact.affineProperty_isLocal.diagonal
#align algebraic_geometry.quasi_separated.affine_property_is_local AlgebraicGeometry.QuasiSeparated.affineProperty_isLocal

instance (priority := 900) quasiSeparatedOfMono {X Y : Scheme} (f : X ‚ü∂ Y) [Mono f] :
    QuasiSeparated f where
#align algebraic_geometry.quasi_separated_of_mono AlgebraicGeometry.quasiSeparatedOfMono

instance quasiSeparated_isStableUnderComposition :
    MorphismProperty.IsStableUnderComposition @QuasiSeparated :=
  quasiSeparated_eq_diagonal_is_quasiCompact.symm ‚ñ∏
    (MorphismProperty.diagonal_isStableUnderComposition
        quasiCompact_respectsIso quasiCompact_stableUnderBaseChange)
#align algebraic_geometry.quasi_separated_stable_under_composition AlgebraicGeometry.quasiSeparated_isStableUnderComposition

theorem quasiSeparated_stableUnderBaseChange :
    MorphismProperty.StableUnderBaseChange @QuasiSeparated :=
  quasiSeparated_eq_diagonal_is_quasiCompact.symm ‚ñ∏
    quasiCompact_stableUnderBaseChange.diagonal quasiCompact_respectsIso
#align algebraic_geometry.quasi_separated_stable_under_base_change AlgebraicGeometry.quasiSeparated_stableUnderBaseChange

instance quasiSeparatedComp {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [QuasiSeparated f]
    [QuasiSeparated g] : QuasiSeparated (f ‚â´ g) :=
  MorphismProperty.comp_mem _ f g inferInstance inferInstance
#align algebraic_geometry.quasi_separated_comp AlgebraicGeometry.quasiSeparatedComp

theorem quasiSeparated_respectsIso : MorphismProperty.RespectsIso @QuasiSeparated :=
  quasiSeparated_eq_diagonal_is_quasiCompact.symm ‚ñ∏ quasiCompact_respectsIso.diagonal
#align algebraic_geometry.quasi_separated_respects_iso AlgebraicGeometry.quasiSeparated_respectsIso

open List in
theorem QuasiSeparated.affine_openCover_TFAE {X Y : Scheme.{u}} (f : X ‚ü∂ Y) :
    TFAE
      [QuasiSeparated f,
        ‚àÉ (ùí∞ : Scheme.OpenCover.{u} Y) (_ : ‚àÄ i, IsAffine (ùí∞.obj i)),
          ‚àÄ i : ùí∞.J, QuasiSeparatedSpace (pullback f (ùí∞.map i)).carrier,
        ‚àÄ (ùí∞ : Scheme.OpenCover.{u} Y) [‚àÄ i, IsAffine (ùí∞.obj i)] (i : ùí∞.J),
          QuasiSeparatedSpace (pullback f (ùí∞.map i)).carrier,
        ‚àÄ {U : Scheme} (g : U ‚ü∂ Y) [IsAffine U] [IsOpenImmersion g],
          QuasiSeparatedSpace (pullback f g).carrier,
        ‚àÉ (ùí∞ : Scheme.OpenCover.{u} Y) (_ : ‚àÄ i, IsAffine (ùí∞.obj i)) (ùí∞' :
          ‚àÄ i : ùí∞.J, Scheme.OpenCover.{u} (pullback f (ùí∞.map i))) (_ :
          ‚àÄ i j, IsAffine ((ùí∞' i).obj j)),
          ‚àÄ (i : ùí∞.J) (j k : (ùí∞' i).J),
            CompactSpace (pullback ((ùí∞' i).map j) ((ùí∞' i).map k)).carrier] := by
  have := QuasiCompact.affineProperty_isLocal.diagonal_affine_openCover_TFAE f
  simp_rw [‚Üê quasiCompact_eq_affineProperty, ‚Üê quasiSeparated_eq_diagonal_is_quasiCompact,
    quasi_compact_affineProperty_diagonal_eq] at this
  exact this
#align algebraic_geometry.quasi_separated.affine_open_cover_tfae AlgebraicGeometry.QuasiSeparated.affine_openCover_TFAE

theorem QuasiSeparated.isLocalAtTarget : PropertyIsLocalAtTarget @QuasiSeparated :=
  quasiSeparated_eq_affineProperty_diagonal.symm ‚ñ∏
    QuasiCompact.affineProperty_isLocal.diagonal.targetAffineLocally_isLocal
#align algebraic_geometry.quasi_separated.is_local_at_target AlgebraicGeometry.QuasiSeparated.isLocalAtTarget

open List in
theorem QuasiSeparated.openCover_TFAE {X Y : Scheme.{u}} (f : X ‚ü∂ Y) :
    TFAE
      [QuasiSeparated f,
        ‚àÉ ùí∞ : Scheme.OpenCover.{u} Y,
          ‚àÄ i : ùí∞.J, QuasiSeparated (pullback.snd : (ùí∞.pullbackCover f).obj i ‚ü∂ ùí∞.obj i),
        ‚àÄ (ùí∞ : Scheme.OpenCover.{u} Y) (i : ùí∞.J),
          QuasiSeparated (pullback.snd : (ùí∞.pullbackCover f).obj i ‚ü∂ ùí∞.obj i),
        ‚àÄ U : Opens Y.carrier, QuasiSeparated (f ‚à£_ U),
        ‚àÄ {U : Scheme} (g : U ‚ü∂ Y) [IsOpenImmersion g],
          QuasiSeparated (pullback.snd : pullback f g ‚ü∂ _),
        ‚àÉ (Œπ : Type u) (U : Œπ ‚Üí Opens Y.carrier) (_ : iSup U = ‚ä§),
          ‚àÄ i, QuasiSeparated (f ‚à£_ U i)] :=
  QuasiSeparated.isLocalAtTarget.openCover_TFAE f
#align algebraic_geometry.quasi_separated.open_cover_tfae AlgebraicGeometry.QuasiSeparated.openCover_TFAE

theorem quasiSeparated_over_affine_iff {X Y : Scheme} (f : X ‚ü∂ Y) [IsAffine Y] :
    QuasiSeparated f ‚Üî QuasiSeparatedSpace X.carrier := by
  rw [quasiSeparated_eq_affineProperty,
    QuasiSeparated.affineProperty_isLocal.affine_target_iff f, QuasiSeparated.affineProperty]
#align algebraic_geometry.quasi_separated_over_affine_iff AlgebraicGeometry.quasiSeparated_over_affine_iff

theorem quasiSeparatedSpace_iff_quasiSeparated (X : Scheme) :
    QuasiSeparatedSpace X.carrier ‚Üî QuasiSeparated (terminal.from X) :=
  (quasiSeparated_over_affine_iff _).symm
#align algebraic_geometry.quasi_separated_space_iff_quasi_separated AlgebraicGeometry.quasiSeparatedSpace_iff_quasiSeparated

theorem QuasiSeparated.affine_openCover_iff {X Y : Scheme.{u}} (ùí∞ : Scheme.OpenCover.{u} Y)
    [‚àÄ i, IsAffine (ùí∞.obj i)] (f : X ‚ü∂ Y) :
    QuasiSeparated f ‚Üî ‚àÄ i, QuasiSeparatedSpace (pullback f (ùí∞.map i)).carrier := by
  rw [quasiSeparated_eq_affineProperty,
    QuasiSeparated.affineProperty_isLocal.affine_openCover_iff f ùí∞]
  rfl
#align algebraic_geometry.quasi_separated.affine_open_cover_iff AlgebraicGeometry.QuasiSeparated.affine_openCover_iff

theorem QuasiSeparated.openCover_iff {X Y : Scheme.{u}} (ùí∞ : Scheme.OpenCover.{u} Y) (f : X ‚ü∂ Y) :
    QuasiSeparated f ‚Üî ‚àÄ i, QuasiSeparated (pullback.snd : pullback f (ùí∞.map i) ‚ü∂ _) :=
  QuasiSeparated.isLocalAtTarget.openCover_iff f ùí∞
#align algebraic_geometry.quasi_separated.open_cover_iff AlgebraicGeometry.QuasiSeparated.openCover_iff

instance {X Y S : Scheme} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [QuasiSeparated g] :
    QuasiSeparated (pullback.fst : pullback f g ‚ü∂ X) :=
  quasiSeparated_stableUnderBaseChange.fst f g inferInstance

instance {X Y S : Scheme} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [QuasiSeparated f] :
    QuasiSeparated (pullback.snd : pullback f g ‚ü∂ Y) :=
  quasiSeparated_stableUnderBaseChange.snd f g inferInstance

instance {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [QuasiSeparated f] [QuasiSeparated g] :
    QuasiSeparated (f ‚â´ g) :=
  MorphismProperty.comp_mem _ f g inferInstance inferInstance

theorem quasiSeparatedSpace_of_quasiSeparated {X Y : Scheme} (f : X ‚ü∂ Y)
    [hY : QuasiSeparatedSpace Y.carrier] [QuasiSeparated f] : QuasiSeparatedSpace X.carrier := by
  rw [quasiSeparatedSpace_iff_quasiSeparated] at hY ‚ä¢
  have : f ‚â´ terminal.from Y = terminal.from X := terminalIsTerminal.hom_ext _ _
  rw [‚Üê this]
  infer_instance
#align algebraic_geometry.quasi_separated_space_of_quasi_separated AlgebraicGeometry.quasiSeparatedSpace_of_quasiSeparated

instance quasiSeparatedSpace_of_isAffine (X : Scheme) [IsAffine X] :
    QuasiSeparatedSpace X.carrier := by
  constructor
  intro U V hU hU' hV hV'
  obtain ‚ü®s, hs, e‚ü© := (isCompact_open_iff_eq_basicOpen_union _).mp ‚ü®hU', hU‚ü©
  obtain ‚ü®s', hs', e'‚ü© := (isCompact_open_iff_eq_basicOpen_union _).mp ‚ü®hV', hV‚ü©
  rw [e, e', Set.iUnion‚ÇÇ_inter]
  simp_rw [Set.inter_iUnion‚ÇÇ]
  apply hs.isCompact_biUnion
  intro i _
  apply hs'.isCompact_biUnion
  intro i' _
  change IsCompact (X.basicOpen i ‚äì X.basicOpen i').1
  rw [‚Üê Scheme.basicOpen_mul]
  exact ((isAffineOpen_top _).basicOpen _).isCompact
#align algebraic_geometry.quasi_separated_space_of_is_affine AlgebraicGeometry.quasiSeparatedSpace_of_isAffine

theorem IsAffineOpen.isQuasiSeparated {X : Scheme} {U : Opens X.carrier} (hU : IsAffineOpen U) :
    IsQuasiSeparated (U : Set X.carrier) := by
  rw [isQuasiSeparated_iff_quasiSeparatedSpace]
  exacts [@AlgebraicGeometry.quasiSeparatedSpace_of_isAffine _ hU, U.isOpen]
#align algebraic_geometry.is_affine_open.is_quasi_separated AlgebraicGeometry.IsAffineOpen.isQuasiSeparated

theorem quasiSeparatedOfComp {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [H : QuasiSeparated (f ‚â´ g)] :
    QuasiSeparated f := by
  -- Porting note: rewrite `(QuasiSeparated.affine_openCover_TFAE f).out 0 1` directly fails, but
  -- give it a name works
  have h01 := (QuasiSeparated.affine_openCover_TFAE f).out 0 1
  rw [h01]; clear h01
  -- Porting note: rewrite `(QuasiSeparated.affine_openCover_TFAE ...).out 0 2` directly fails, but
  -- give it a name works
  have h02 := (QuasiSeparated.affine_openCover_TFAE (f ‚â´ g)).out 0 2
  rw [h02] at H; clear h02
  refine ‚ü®(Z.affineCover.pullbackCover g).bind fun x => Scheme.affineCover _, ?_, ?_‚ü©
  -- constructor
  ¬∑ intro i; dsimp; infer_instance
  rintro ‚ü®i, j‚ü©; dsimp at i j
  -- replace H := H (Scheme.OpenCover.pullbackCover (Scheme.affineCover Z) g) i
  specialize H _ i
  -- rw [‚Üê isQuasiSeparated_iff_quasiSeparatedSpace] at H
  refine @quasiSeparatedSpace_of_quasiSeparated _ _ ?_ H ?_
  ¬∑ exact pullback.map _ _ _ _ (ùüô _) _ _ (by simp) (Category.comp_id _) ‚â´
      (pullbackRightPullbackFstIso g (Z.affineCover.map i) f).hom
  ¬∑ exact inferInstance
#align algebraic_geometry.quasi_separated_of_comp AlgebraicGeometry.quasiSeparatedOfComp

theorem exists_eq_pow_mul_of_isAffineOpen (X : Scheme) (U : Opens X.carrier) (hU : IsAffineOpen U)
    (f : X.presheaf.obj (op U)) (x : X.presheaf.obj (op <| X.basicOpen f)) :
    ‚àÉ (n : ‚Ñï) (y : X.presheaf.obj (op U)), y |_ X.basicOpen f = (f |_ X.basicOpen f) ^ n * x := by
  have := (hU.isLocalization_basicOpen f).2
  obtain ‚ü®‚ü®y, _, n, rfl‚ü©, d‚ü© := this x
  use n, y
  delta TopCat.Presheaf.restrictOpen TopCat.Presheaf.restrict
  simpa [mul_comm x] using d.symm
#align algebraic_geometry.exists_eq_pow_mul_of_is_affine_open AlgebraicGeometry.exists_eq_pow_mul_of_isAffineOpen

theorem exists_eq_pow_mul_of_is_compact_of_quasi_separated_space_aux_aux {X : TopCat}
    (F : X.Presheaf CommRingCat) {U‚ÇÅ U‚ÇÇ U‚ÇÉ U‚ÇÑ U‚ÇÖ U‚ÇÜ U‚Çá : Opens X} {n‚ÇÅ n‚ÇÇ : ‚Ñï}
    {y‚ÇÅ : F.obj (op U‚ÇÅ)} {y‚ÇÇ : F.obj (op U‚ÇÇ)} {f : F.obj (op <| U‚ÇÅ ‚äî U‚ÇÇ)}
    {x : F.obj (op U‚ÇÉ)} (h‚ÇÑ‚ÇÅ : U‚ÇÑ ‚â§ U‚ÇÅ) (h‚ÇÑ‚ÇÇ : U‚ÇÑ ‚â§ U‚ÇÇ) (h‚ÇÖ‚ÇÅ : U‚ÇÖ ‚â§ U‚ÇÅ) (h‚ÇÖ‚ÇÉ : U‚ÇÖ ‚â§ U‚ÇÉ)
    (h‚ÇÜ‚ÇÇ : U‚ÇÜ ‚â§ U‚ÇÇ) (h‚ÇÜ‚ÇÉ : U‚ÇÜ ‚â§ U‚ÇÉ) (h‚Çá‚ÇÑ : U‚Çá ‚â§ U‚ÇÑ) (h‚Çá‚ÇÖ : U‚Çá ‚â§ U‚ÇÖ) (h‚Çá‚ÇÜ : U‚Çá ‚â§ U‚ÇÜ)
    (e‚ÇÅ : y‚ÇÅ |_ U‚ÇÖ = (f |_ U‚ÇÅ |_ U‚ÇÖ) ^ n‚ÇÅ * x |_ U‚ÇÖ)
    (e‚ÇÇ : y‚ÇÇ |_ U‚ÇÜ = (f |_ U‚ÇÇ |_ U‚ÇÜ) ^ n‚ÇÇ * x |_ U‚ÇÜ) :
    (((f |_ U‚ÇÅ) ^ n‚ÇÇ * y‚ÇÅ) |_ U‚ÇÑ) |_ U‚Çá = (((f |_ U‚ÇÇ) ^ n‚ÇÅ * y‚ÇÇ) |_ U‚ÇÑ) |_ U‚Çá := by
  apply_fun (fun x : F.obj (op U‚ÇÖ) ‚Ü¶ x |_ U‚Çá) at e‚ÇÅ
  apply_fun (fun x : F.obj (op U‚ÇÜ) ‚Ü¶ x |_ U‚Çá) at e‚ÇÇ
  dsimp only [TopCat.Presheaf.restrictOpen, TopCat.Presheaf.restrict] at e‚ÇÅ e‚ÇÇ ‚ä¢
  simp only [map_mul, map_pow, ‚Üê comp_apply, ‚Üê op_comp, ‚Üê F.map_comp, homOfLE_comp] at e‚ÇÅ e‚ÇÇ ‚ä¢
  rw [e‚ÇÅ, e‚ÇÇ, mul_left_comm]

theorem exists_eq_pow_mul_of_is_compact_of_quasi_separated_space_aux (X : Scheme)
    (S : X.affineOpens) (U‚ÇÅ U‚ÇÇ : Opens X.carrier) {n‚ÇÅ n‚ÇÇ : ‚Ñï} {y‚ÇÅ : X.presheaf.obj (op U‚ÇÅ)}
    {y‚ÇÇ : X.presheaf.obj (op U‚ÇÇ)} {f : X.presheaf.obj (op <| U‚ÇÅ ‚äî U‚ÇÇ)}
    {x : X.presheaf.obj (op <| X.basicOpen f)} (h‚ÇÅ : S.1 ‚â§ U‚ÇÅ) (h‚ÇÇ : S.1 ‚â§ U‚ÇÇ)
    (e‚ÇÅ : y‚ÇÅ |_ X.basicOpen (f |_ U‚ÇÅ) = ((f |_ U‚ÇÅ |_ X.basicOpen _) ^ n‚ÇÅ) * x |_ X.basicOpen _)
    (e‚ÇÇ : y‚ÇÇ |_ X.basicOpen (f |_ U‚ÇÇ) = ((f |_ U‚ÇÇ |_ X.basicOpen _) ^ n‚ÇÇ) * x |_ X.basicOpen _) :
    ‚àÉ n : ‚Ñï, ‚àÄ m, n ‚â§ m ‚Üí
      ((f |_ U‚ÇÅ) ^ (m + n‚ÇÇ) * y‚ÇÅ) |_ S.1 = ((f |_ U‚ÇÇ) ^ (m + n‚ÇÅ) * y‚ÇÇ) |_ S.1 := by
  obtain ‚ü®‚ü®_, n, rfl‚ü©, e‚ü© :=
    (@IsLocalization.eq_iff_exists _ _ _ _ _ _
      (S.2.isLocalization_basicOpen (f |_ S.1))
        (((f |_ U‚ÇÅ) ^ n‚ÇÇ * y‚ÇÅ) |_ S.1)
        (((f |_ U‚ÇÇ) ^ n‚ÇÅ * y‚ÇÇ) |_ S.1)).mp <| by
    apply exists_eq_pow_mul_of_is_compact_of_quasi_separated_space_aux_aux (e‚ÇÅ := e‚ÇÅ) (e‚ÇÇ := e‚ÇÇ)
    ¬∑ show X.basicOpen _ ‚â§ _
      simp only [TopCat.Presheaf.restrictOpen, TopCat.Presheaf.restrict]
      repeat rw [Scheme.basicOpen_res] -- Note: used to be part of the `simp only`
      exact inf_le_inf h‚ÇÅ le_rfl
    ¬∑ show X.basicOpen _ ‚â§ _
      simp only [TopCat.Presheaf.restrictOpen, TopCat.Presheaf.restrict]
      repeat rw [Scheme.basicOpen_res] -- Note: used to be part of the `simp only`
      exact inf_le_inf h‚ÇÇ le_rfl
  use n
  intros m hm
  rw [‚Üê tsub_add_cancel_of_le hm]
  simp only [TopCat.Presheaf.restrictOpen, TopCat.Presheaf.restrict,
    pow_add, map_pow, map_mul, ‚Üê comp_apply, mul_assoc, ‚Üê Functor.map_comp, ‚Üê op_comp, homOfLE_comp,
    Subtype.coe_mk] at e ‚ä¢
  rw [e]
#align algebraic_geometry.exists_eq_pow_mul_of_is_compact_of_quasi_separated_space_aux AlgebraicGeometry.exists_eq_pow_mul_of_is_compact_of_quasi_separated_space_aux

theorem exists_eq_pow_mul_of_isCompact_of_isQuasiSeparated (X : Scheme.{u}) (U : Opens X.carrier)
    (hU : IsCompact U.1) (hU' : IsQuasiSeparated U.1) (f : X.presheaf.obj (op U))
    (x : X.presheaf.obj (op <| X.basicOpen f)) :
    ‚àÉ (n : ‚Ñï) (y : X.presheaf.obj (op U)), y |_ X.basicOpen f = (f |_ X.basicOpen f) ^ n * x := by
  delta TopCat.Presheaf.restrictOpen TopCat.Presheaf.restrict
  revert hU' f x
  -- Porting note: complains `expected type is not available`, but tell Lean that it is underscore
  -- is sufficient
  apply compact_open_induction_on (P := _) U hU
  ¬∑ intro _ f x
    use 0, f
    refine @Subsingleton.elim _
      (CommRingCat.subsingleton_of_isTerminal (X.sheaf.isTerminalOfEqEmpty ?_)) _ _
    erw [eq_bot_iff]
    exact X.basicOpen_le f
  ¬∑ -- Given `f : ùí™(S ‚à™ U), x : ùí™(X_f)`, we need to show that `f ^ n * x` is the restriction of
    -- some `y : ùí™(S ‚à™ U)` for some `n : ‚Ñï`.
    intro S hS U hU hSU f x
    -- We know that such `y‚ÇÅ, n‚ÇÅ` exists on `S` by the induction hypothesis.
    obtain ‚ü®n‚ÇÅ, y‚ÇÅ, hy‚ÇÅ‚ü© :=
      hU (hSU.of_subset Set.subset_union_left) (X.presheaf.map (homOfLE le_sup_left).op f)
        (X.presheaf.map (homOfLE _).op x)
    -- ¬∑ rw [X.basicOpen_res]; exact inf_le_right
    -- We know that such `y‚ÇÇ, n‚ÇÇ` exists on `U` since `U` is affine.
    obtain ‚ü®n‚ÇÇ, y‚ÇÇ, hy‚ÇÇ‚ü© :=
      exists_eq_pow_mul_of_isAffineOpen X _ U.2 (X.presheaf.map (homOfLE le_sup_right).op f)
        (X.presheaf.map (homOfLE _).op x)
    delta TopCat.Presheaf.restrictOpen TopCat.Presheaf.restrict at hy‚ÇÇ
    -- swap; ¬∑ rw [X.basicOpen_res]; exact inf_le_right
    -- Since `S ‚à™ U` is quasi-separated, `S ‚à© U` can be covered by finite affine opens.
    obtain ‚ü®s, hs', hs‚ü© :=
      (isCompact_open_iff_eq_finset_affine_union _).mp
        ‚ü®hSU _ _ Set.subset_union_left S.2 hS Set.subset_union_right U.1.2
            U.2.isCompact,
          (S ‚äì U.1).2‚ü©
    haveI := hs'.to_subtype
    cases nonempty_fintype s
    replace hs : S ‚äì U.1 = iSup fun i : s => (i : Opens X.carrier) := by ext1; simpa using hs
    have hs‚ÇÅ : ‚àÄ i : s, i.1.1 ‚â§ S := by
      intro i; change (i : Opens X.carrier) ‚â§ S
      refine le_trans ?_ (inf_le_left (b := U.1))
      erw [hs]
      -- Porting note: have to add argument explicitly
      exact @le_iSup (Opens X) s _ (fun (i : s) => (i : Opens X)) i
    have hs‚ÇÇ : ‚àÄ i : s, i.1.1 ‚â§ U.1 := by
      intro i; change (i : Opens X.carrier) ‚â§ U
      refine le_trans ?_ (inf_le_right (a := S))
      erw [hs]
      -- Porting note: have to add argument explicitly
      exact @le_iSup (Opens X) s _ (fun (i : s) => (i : Opens X)) i
    -- On each affine open in the intersection, we have `f ^ (n + n‚ÇÇ) * y‚ÇÅ = f ^ (n + n‚ÇÅ) * y‚ÇÇ`
    -- for some `n` since `f ^ n‚ÇÇ * y‚ÇÅ = f ^ (n‚ÇÅ + n‚ÇÇ) * x = f ^ n‚ÇÅ * y‚ÇÇ` on `X_f`.
    have := fun i ‚Ü¶ exists_eq_pow_mul_of_is_compact_of_quasi_separated_space_aux
      X i.1 S U (hs‚ÇÅ i) (hs‚ÇÇ i) hy‚ÇÅ hy‚ÇÇ
    choose n hn using this
    -- We can thus choose a big enough `n` such that `f ^ (n + n‚ÇÇ) * y‚ÇÅ = f ^ (n + n‚ÇÅ) * y‚ÇÇ`
    -- on `S ‚à© U`.
    have :
      X.presheaf.map (homOfLE <| inf_le_left).op
          (X.presheaf.map (homOfLE le_sup_left).op f ^ (Finset.univ.sup n + n‚ÇÇ) * y‚ÇÅ) =
        X.presheaf.map (homOfLE <| inf_le_right).op
          (X.presheaf.map (homOfLE le_sup_right).op f ^ (Finset.univ.sup n + n‚ÇÅ) * y‚ÇÇ) := by
      fapply X.sheaf.eq_of_locally_eq' fun i : s => i.1.1
      ¬∑ refine fun i => homOfLE ?_; erw [hs];
        -- Porting note: have to add argument explicitly
        exact @le_iSup (Opens X) s _ (fun (i : s) => (i : Opens X)) i
      ¬∑ exact le_of_eq hs
      ¬∑ intro i
        delta Scheme.sheaf SheafedSpace.sheaf
        repeat rw [‚Üê comp_apply,]
        simp only [‚Üê Functor.map_comp, ‚Üê op_comp]
        apply hn
        exact Finset.le_sup (Finset.mem_univ _)
    use Finset.univ.sup n + n‚ÇÅ + n‚ÇÇ
    -- By the sheaf condition, since `f ^ (n + n‚ÇÇ) * y‚ÇÅ = f ^ (n + n‚ÇÅ) * y‚ÇÇ`, it can be glued into
    -- the desired section on `S ‚à™ U`.
    use (X.sheaf.objSupIsoProdEqLocus S U.1).inv ‚ü®‚ü®_ * _, _ * _‚ü©, this‚ü©
    refine (X.sheaf.objSupIsoProdEqLocus_inv_eq_iff _ _ _ (X.basicOpen_res _
      (homOfLE le_sup_left).op) (X.basicOpen_res _ (homOfLE le_sup_right).op)).mpr ‚ü®?_, ?_‚ü©
    ¬∑ delta Scheme.sheaf SheafedSpace.sheaf
      rw [add_assoc, add_comm n‚ÇÅ]
      simp only [pow_add, map_pow, map_mul]
      rw [hy‚ÇÅ] -- Note: `simp` can't use this
      repeat rw [‚Üê comp_apply] -- Note: `simp` can't use this
      simp only [‚Üê mul_assoc, ‚Üê Functor.map_comp, ‚Üê op_comp, homOfLE_comp]
    ¬∑ delta Scheme.sheaf SheafedSpace.sheaf
      simp only [pow_add, map_pow, map_mul]
      rw [hy‚ÇÇ] -- Note: `simp` can't use this
      repeat rw [‚Üê comp_apply] -- Note: `simp` can't use this
      simp only [‚Üê mul_assoc, ‚Üê Functor.map_comp, ‚Üê op_comp, homOfLE_comp]
#align algebraic_geometry.exists_eq_pow_mul_of_is_compact_of_is_quasi_separated AlgebraicGeometry.exists_eq_pow_mul_of_isCompact_of_isQuasiSeparated

/-- If `U` is qcqs, then `Œì(X, D(f)) ‚âÉ Œì(X, U)_f` for every `f : Œì(X, U)`.
This is known as the **Qcqs lemma** in [R. Vakil, *The rising sea*][RisingSea]. -/
theorem is_localization_basicOpen_of_qcqs {X : Scheme} {U : Opens X.carrier} (hU : IsCompact U.1)
    (hU' : IsQuasiSeparated U.1) (f : X.presheaf.obj (op U)) :
    IsLocalization.Away f (X.presheaf.obj (op <| X.basicOpen f)) := by
  constructor
  ¬∑ rintro ‚ü®_, n, rfl‚ü©
    simp only [map_pow, Subtype.coe_mk, RingHom.algebraMap_toAlgebra]
    exact IsUnit.pow _ (RingedSpace.isUnit_res_basicOpen _ f)
  ¬∑ intro z
    obtain ‚ü®n, y, e‚ü© := exists_eq_pow_mul_of_isCompact_of_isQuasiSeparated X U hU hU' f z
    refine ‚ü®‚ü®y, _, n, rfl‚ü©, ?_‚ü©
    simpa only [map_pow, Subtype.coe_mk, RingHom.algebraMap_toAlgebra, mul_comm z] using e.symm
  ¬∑ intro x y
    rw [‚Üê sub_eq_zero, ‚Üê map_sub, RingHom.algebraMap_toAlgebra]
    simp_rw [‚Üê @sub_eq_zero _ _ (_ * x) (_ * y), ‚Üê mul_sub]
    generalize x - y = z
    intro H
    obtain ‚ü®n, e‚ü© := exists_pow_mul_eq_zero_of_res_basicOpen_eq_zero_of_isCompact X hU _ _ H
    refine ‚ü®‚ü®_, n, rfl‚ü©, ?_‚ü©
    simpa [mul_comm z] using e
#align algebraic_geometry.is_localization_basic_open_of_qcqs AlgebraicGeometry.is_localization_basicOpen_of_qcqs

end AlgebraicGeometry
