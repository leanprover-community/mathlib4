/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.QuasiCompact
import Mathlib.Topology.QuasiSeparated

#align_import algebraic_geometry.morphisms.quasi_separated from "leanprover-community/mathlib"@"1a51edf13debfcbe223fa06b1cb353b9ed9751cc"

/-!
# Quasi-separated morphisms

A morphism of schemes `f : X âŸ¶ Y` is quasi-separated if the diagonal morphism `X âŸ¶ X Ã—[Y] X` is
quasi-compact.

A scheme is quasi-separated if the intersections of any two affine open sets is quasi-compact.
(`AlgebraicGeometry.quasiSeparatedSpace_iff_affine`)

We show that a morphism is quasi-separated if the preimage of every affine open is quasi-separated.

We also show that this property is local at the target,
and is stable under compositions and base-changes.

## Main result
- `AlgebraicGeometry.is_localization_basicOpen_of_qcqs` (**Qcqs lemma**):
  If `U` is qcqs, then `Î“(X, D(f)) â‰ƒ Î“(X, U)_f` for every `f : Î“(X, U)`.

-/


noncomputable section

open CategoryTheory CategoryTheory.Limits Opposite TopologicalSpace

universe u

open scoped AlgebraicGeometry

namespace AlgebraicGeometry

variable {X Y : Scheme.{u}} (f : X âŸ¶ Y)

/-- A morphism is `QuasiSeparated` if diagonal map is quasi-compact. -/
@[mk_iff]
class QuasiSeparated (f : X âŸ¶ Y) : Prop where
  /-- A morphism is `QuasiSeparated` if diagonal map is quasi-compact. -/
  diagonalQuasiCompact : QuasiCompact (pullback.diagonal f)
#align algebraic_geometry.quasi_separated AlgebraicGeometry.QuasiSeparated

/-- The `AffineTargetMorphismProperty` corresponding to `QuasiSeparated`, asserting that the
domain is a quasi-separated scheme. -/
def QuasiSeparated.affineProperty : AffineTargetMorphismProperty := fun X _ _ _ =>
  QuasiSeparatedSpace X.carrier
#align algebraic_geometry.quasi_separated.affine_property AlgebraicGeometry.QuasiSeparated.affineProperty

theorem quasiSeparatedSpace_iff_affine (X : Scheme) :
    QuasiSeparatedSpace X.carrier â†” âˆ€ U V : X.affineOpens, IsCompact (U âˆ© V : Set X.carrier) := by
  rw [QuasiSeparatedSpace_iff]
  constructor
  Â· intro H U V; exact H U V U.1.2 U.2.isCompact V.1.2 V.2.isCompact
  Â· intro H
    suffices
      âˆ€ (U : Opens X.carrier) (_ : IsCompact U.1) (V : Opens X.carrier) (_ : IsCompact V.1),
        IsCompact (U âŠ“ V).1
      by intro U V hU hU' hV hV'; exact this âŸ¨U, hUâŸ© hU' âŸ¨V, hVâŸ© hV'
    intro U hU V hV
    -- Porting note: it complains "unable to find motive", but telling Lean that motive is
    -- underscore is actually sufficient, weird
    apply compact_open_induction_on (P := _) V hV
    Â· simp
    Â· intro S _ V hV
      change IsCompact (U.1 âˆ© (S.1 âˆª V.1))
      rw [Set.inter_union_distrib_left]
      apply hV.union
      clear hV
      apply compact_open_induction_on (P := _) U hU
      Â· simp
      Â· intro S _ W hW
        change IsCompact ((S.1 âˆª W.1) âˆ© V.1)
        rw [Set.union_inter_distrib_right]
        apply hW.union
        apply H
#align algebraic_geometry.quasi_separated_space_iff_affine AlgebraicGeometry.quasiSeparatedSpace_iff_affine

theorem quasi_compact_affineProperty_iff_quasiSeparatedSpace {X Y : Scheme} [IsAffine Y]
    (f : X âŸ¶ Y) : QuasiCompact.affineProperty.diagonal f â†” QuasiSeparatedSpace X.carrier := by
  delta AffineTargetMorphismProperty.diagonal
  rw [quasiSeparatedSpace_iff_affine]
  constructor
  Â· intro H U V
    haveI : IsAffine _ := U.2
    haveI : IsAffine _ := V.2
    let g : pullback (X.ofRestrict U.1.openEmbedding) (X.ofRestrict V.1.openEmbedding) âŸ¶ X :=
      pullback.fst â‰« X.ofRestrict _
    -- Porting note: `inferInstance` does not work here
    have : IsOpenImmersion g := PresheafedSpace.IsOpenImmersion.comp _ _
    have e := Homeomorph.ofEmbedding _ this.base_open.toEmbedding
    rw [IsOpenImmersion.range_pullback_to_base_of_left] at e
    erw [Subtype.range_coe, Subtype.range_coe] at e
    rw [isCompact_iff_compactSpace]
    exact @Homeomorph.compactSpace _ _ _ _ (H _ _) e
  Â· introv H hâ‚ hâ‚‚
    let g : pullback fâ‚ fâ‚‚ âŸ¶ X := pullback.fst â‰« fâ‚
    -- Porting note: `inferInstance` does not work here
    have : IsOpenImmersion g := PresheafedSpace.IsOpenImmersion.comp _ _
    have e := Homeomorph.ofEmbedding _ this.base_open.toEmbedding
    rw [IsOpenImmersion.range_pullback_to_base_of_left] at e
    simp_rw [isCompact_iff_compactSpace] at H
    exact
      @Homeomorph.compactSpace _ _ _ _
        (H âŸ¨âŸ¨_, hâ‚.base_open.open_rangeâŸ©, rangeIsAffineOpenOfOpenImmersion _âŸ©
          âŸ¨âŸ¨_, hâ‚‚.base_open.open_rangeâŸ©, rangeIsAffineOpenOfOpenImmersion _âŸ©)
        e.symm
#align algebraic_geometry.quasi_compact_affine_property_iff_quasi_separated_space AlgebraicGeometry.quasi_compact_affineProperty_iff_quasiSeparatedSpace

theorem quasiSeparated_eq_diagonal_is_quasiCompact :
    @QuasiSeparated = MorphismProperty.diagonal @QuasiCompact := by ext; exact QuasiSeparated_iff _
#align algebraic_geometry.quasi_separated_eq_diagonal_is_quasi_compact AlgebraicGeometry.quasiSeparated_eq_diagonal_is_quasiCompact

theorem quasi_compact_affineProperty_diagonal_eq :
    QuasiCompact.affineProperty.diagonal = QuasiSeparated.affineProperty := by
  funext; rw [quasi_compact_affineProperty_iff_quasiSeparatedSpace]; rfl
#align algebraic_geometry.quasi_compact_affine_property_diagonal_eq AlgebraicGeometry.quasi_compact_affineProperty_diagonal_eq

theorem quasiSeparated_eq_affineProperty_diagonal :
    @QuasiSeparated = targetAffineLocally QuasiCompact.affineProperty.diagonal := by
  rw [quasiSeparated_eq_diagonal_is_quasiCompact, quasiCompact_eq_affineProperty]
  exact
    diagonal_targetAffineLocally_eq_targetAffineLocally _ QuasiCompact.affineProperty_isLocal
#align algebraic_geometry.quasi_separated_eq_affine_property_diagonal AlgebraicGeometry.quasiSeparated_eq_affineProperty_diagonal

theorem quasiSeparated_eq_affineProperty :
    @QuasiSeparated = targetAffineLocally QuasiSeparated.affineProperty := by
  rw [quasiSeparated_eq_affineProperty_diagonal, quasi_compact_affineProperty_diagonal_eq]
#align algebraic_geometry.quasi_separated_eq_affine_property AlgebraicGeometry.quasiSeparated_eq_affineProperty

theorem QuasiSeparated.affineProperty_isLocal : QuasiSeparated.affineProperty.IsLocal :=
  quasi_compact_affineProperty_diagonal_eq â–¸ QuasiCompact.affineProperty_isLocal.diagonal
#align algebraic_geometry.quasi_separated.affine_property_is_local AlgebraicGeometry.QuasiSeparated.affineProperty_isLocal

instance (priority := 900) quasiSeparatedOfMono {X Y : Scheme} (f : X âŸ¶ Y) [Mono f] :
    QuasiSeparated f :=
  âŸ¨inferInstanceâŸ©
#align algebraic_geometry.quasi_separated_of_mono AlgebraicGeometry.quasiSeparatedOfMono

theorem quasiSeparated_stableUnderComposition :
    MorphismProperty.StableUnderComposition @QuasiSeparated :=
  quasiSeparated_eq_diagonal_is_quasiCompact.symm â–¸
    quasiCompact_stableUnderComposition.diagonal quasiCompact_respectsIso
      quasiCompact_stableUnderBaseChange
#align algebraic_geometry.quasi_separated_stable_under_composition AlgebraicGeometry.quasiSeparated_stableUnderComposition

theorem quasiSeparated_stableUnderBaseChange :
    MorphismProperty.StableUnderBaseChange @QuasiSeparated :=
  quasiSeparated_eq_diagonal_is_quasiCompact.symm â–¸
    quasiCompact_stableUnderBaseChange.diagonal quasiCompact_respectsIso
#align algebraic_geometry.quasi_separated_stable_under_base_change AlgebraicGeometry.quasiSeparated_stableUnderBaseChange

instance quasiSeparatedComp {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [QuasiSeparated f]
    [QuasiSeparated g] : QuasiSeparated (f â‰« g) :=
  quasiSeparated_stableUnderComposition f g inferInstance inferInstance
#align algebraic_geometry.quasi_separated_comp AlgebraicGeometry.quasiSeparatedComp

theorem quasiSeparated_respectsIso : MorphismProperty.RespectsIso @QuasiSeparated :=
  quasiSeparated_eq_diagonal_is_quasiCompact.symm â–¸ quasiCompact_respectsIso.diagonal
#align algebraic_geometry.quasi_separated_respects_iso AlgebraicGeometry.quasiSeparated_respectsIso

open List in
theorem QuasiSeparated.affine_openCover_TFAE {X Y : Scheme.{u}} (f : X âŸ¶ Y) :
    TFAE
      [QuasiSeparated f,
        âˆƒ (ğ’° : Scheme.OpenCover.{u} Y) (_ : âˆ€ i, IsAffine (ğ’°.obj i)),
          âˆ€ i : ğ’°.J, QuasiSeparatedSpace (pullback f (ğ’°.map i)).carrier,
        âˆ€ (ğ’° : Scheme.OpenCover.{u} Y) [âˆ€ i, IsAffine (ğ’°.obj i)] (i : ğ’°.J),
          QuasiSeparatedSpace (pullback f (ğ’°.map i)).carrier,
        âˆ€ {U : Scheme} (g : U âŸ¶ Y) [IsAffine U] [IsOpenImmersion g],
          QuasiSeparatedSpace (pullback f g).carrier,
        âˆƒ (ğ’° : Scheme.OpenCover.{u} Y) (_ : âˆ€ i, IsAffine (ğ’°.obj i)) (ğ’°' :
          âˆ€ i : ğ’°.J, Scheme.OpenCover.{u} (pullback f (ğ’°.map i))) (_ :
          âˆ€ i j, IsAffine ((ğ’°' i).obj j)),
          âˆ€ (i : ğ’°.J) (j k : (ğ’°' i).J),
            CompactSpace (pullback ((ğ’°' i).map j) ((ğ’°' i).map k)).carrier] := by
  have := QuasiCompact.affineProperty_isLocal.diagonal_affine_openCover_TFAE f
  simp_rw [â† quasiCompact_eq_affineProperty, â† quasiSeparated_eq_diagonal_is_quasiCompact,
    quasi_compact_affineProperty_diagonal_eq] at this
  exact this
#align algebraic_geometry.quasi_separated.affine_open_cover_tfae AlgebraicGeometry.QuasiSeparated.affine_openCover_TFAE

theorem QuasiSeparated.is_local_at_target : PropertyIsLocalAtTarget @QuasiSeparated :=
  quasiSeparated_eq_affineProperty_diagonal.symm â–¸
    QuasiCompact.affineProperty_isLocal.diagonal.targetAffineLocallyIsLocal
#align algebraic_geometry.quasi_separated.is_local_at_target AlgebraicGeometry.QuasiSeparated.is_local_at_target

open List in
theorem QuasiSeparated.openCover_TFAE {X Y : Scheme.{u}} (f : X âŸ¶ Y) :
    TFAE
      [QuasiSeparated f,
        âˆƒ ğ’° : Scheme.OpenCover.{u} Y,
          âˆ€ i : ğ’°.J, QuasiSeparated (pullback.snd : (ğ’°.pullbackCover f).obj i âŸ¶ ğ’°.obj i),
        âˆ€ (ğ’° : Scheme.OpenCover.{u} Y) (i : ğ’°.J),
          QuasiSeparated (pullback.snd : (ğ’°.pullbackCover f).obj i âŸ¶ ğ’°.obj i),
        âˆ€ U : Opens Y.carrier, QuasiSeparated (f âˆ£_ U),
        âˆ€ {U : Scheme} (g : U âŸ¶ Y) [IsOpenImmersion g],
          QuasiSeparated (pullback.snd : pullback f g âŸ¶ _),
        âˆƒ (Î¹ : Type u) (U : Î¹ â†’ Opens Y.carrier) (_ : iSup U = âŠ¤),
          âˆ€ i, QuasiSeparated (f âˆ£_ U i)] :=
  QuasiSeparated.is_local_at_target.openCover_TFAE f
#align algebraic_geometry.quasi_separated.open_cover_tfae AlgebraicGeometry.QuasiSeparated.openCover_TFAE

theorem quasiSeparated_over_affine_iff {X Y : Scheme} (f : X âŸ¶ Y) [IsAffine Y] :
    QuasiSeparated f â†” QuasiSeparatedSpace X.carrier := by
  rw [quasiSeparated_eq_affineProperty,
    QuasiSeparated.affineProperty_isLocal.affine_target_iff f, QuasiSeparated.affineProperty]
#align algebraic_geometry.quasi_separated_over_affine_iff AlgebraicGeometry.quasiSeparated_over_affine_iff

theorem quasiSeparatedSpace_iff_quasiSeparated (X : Scheme) :
    QuasiSeparatedSpace X.carrier â†” QuasiSeparated (terminal.from X) :=
  (quasiSeparated_over_affine_iff _).symm
#align algebraic_geometry.quasi_separated_space_iff_quasi_separated AlgebraicGeometry.quasiSeparatedSpace_iff_quasiSeparated

theorem QuasiSeparated.affine_openCover_iff {X Y : Scheme.{u}} (ğ’° : Scheme.OpenCover.{u} Y)
    [âˆ€ i, IsAffine (ğ’°.obj i)] (f : X âŸ¶ Y) :
    QuasiSeparated f â†” âˆ€ i, QuasiSeparatedSpace (pullback f (ğ’°.map i)).carrier := by
  rw [quasiSeparated_eq_affineProperty,
    QuasiSeparated.affineProperty_isLocal.affine_openCover_iff f ğ’°]
  rfl
#align algebraic_geometry.quasi_separated.affine_open_cover_iff AlgebraicGeometry.QuasiSeparated.affine_openCover_iff

theorem QuasiSeparated.openCover_iff {X Y : Scheme.{u}} (ğ’° : Scheme.OpenCover.{u} Y) (f : X âŸ¶ Y) :
    QuasiSeparated f â†” âˆ€ i, QuasiSeparated (pullback.snd : pullback f (ğ’°.map i) âŸ¶ _) :=
  QuasiSeparated.is_local_at_target.openCover_iff f ğ’°
#align algebraic_geometry.quasi_separated.open_cover_iff AlgebraicGeometry.QuasiSeparated.openCover_iff

instance {X Y S : Scheme} (f : X âŸ¶ S) (g : Y âŸ¶ S) [QuasiSeparated g] :
    QuasiSeparated (pullback.fst : pullback f g âŸ¶ X) :=
  quasiSeparated_stableUnderBaseChange.fst f g inferInstance

instance {X Y S : Scheme} (f : X âŸ¶ S) (g : Y âŸ¶ S) [QuasiSeparated f] :
    QuasiSeparated (pullback.snd : pullback f g âŸ¶ Y) :=
  quasiSeparated_stableUnderBaseChange.snd f g inferInstance

instance {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [QuasiSeparated f] [QuasiSeparated g] :
    QuasiSeparated (f â‰« g) :=
  quasiSeparated_stableUnderComposition f g inferInstance inferInstance

theorem quasiSeparatedSpace_of_quasiSeparated {X Y : Scheme} (f : X âŸ¶ Y)
    [hY : QuasiSeparatedSpace Y.carrier] [QuasiSeparated f] : QuasiSeparatedSpace X.carrier := by
  rw [quasiSeparatedSpace_iff_quasiSeparated] at hY âŠ¢
  have : f â‰« terminal.from Y = terminal.from X := terminalIsTerminal.hom_ext _ _
  rw [â† this]
  infer_instance
#align algebraic_geometry.quasi_separated_space_of_quasi_separated AlgebraicGeometry.quasiSeparatedSpace_of_quasiSeparated

instance quasiSeparatedSpace_of_isAffine (X : Scheme) [IsAffine X] :
    QuasiSeparatedSpace X.carrier := by
  constructor
  intro U V hU hU' hV hV'
  obtain âŸ¨s, hs, eâŸ© := (isCompact_open_iff_eq_basicOpen_union _).mp âŸ¨hU', hUâŸ©
  obtain âŸ¨s', hs', e'âŸ© := (isCompact_open_iff_eq_basicOpen_union _).mp âŸ¨hV', hVâŸ©
  rw [e, e', Set.iUnionâ‚‚_inter]
  simp_rw [Set.inter_iUnionâ‚‚]
  apply hs.isCompact_biUnion
  Â· intro i _
    apply hs'.isCompact_biUnion
    intro i' _
    change IsCompact (X.basicOpen i âŠ“ X.basicOpen i').1
    rw [â† Scheme.basicOpen_mul]
    exact ((topIsAffineOpen _).basicOpenIsAffine _).isCompact
#align algebraic_geometry.quasi_separated_space_of_is_affine AlgebraicGeometry.quasiSeparatedSpace_of_isAffine

theorem IsAffineOpen.isQuasiSeparated {X : Scheme} {U : Opens X.carrier} (hU : IsAffineOpen U) :
    IsQuasiSeparated (U : Set X.carrier) := by
  rw [isQuasiSeparated_iff_quasiSeparatedSpace]
  exacts [@AlgebraicGeometry.quasiSeparatedSpace_of_isAffine _ hU, U.isOpen]
#align algebraic_geometry.is_affine_open.is_quasi_separated AlgebraicGeometry.IsAffineOpen.isQuasiSeparated

theorem quasiSeparatedOfComp {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [H : QuasiSeparated (f â‰« g)] :
    QuasiSeparated f := by
  -- Porting note: rewrite `(QuasiSeparated.affine_openCover_TFAE f).out 0 1` directly fails, but
  -- give it a name works
  have h01 := (QuasiSeparated.affine_openCover_TFAE f).out 0 1
  rw [h01]; clear h01
  -- Porting note: rewrite `(QuasiSeparated.affine_openCover_TFAE ...).out 0 2` directly fails, but
  -- give it a name works
  have h02 := (QuasiSeparated.affine_openCover_TFAE (f â‰« g)).out 0 2
  rw [h02] at H; clear h02
  refine âŸ¨(Z.affineCover.pullbackCover g).bind fun x => Scheme.affineCover _, ?_, ?_âŸ©
  -- constructor
  Â· intro i; dsimp; infer_instance
  rintro âŸ¨i, jâŸ©; dsimp at i j
  -- replace H := H (Scheme.OpenCover.pullbackCover (Scheme.affineCover Z) g) i
  specialize H _ i
  -- rw [â†isQuasiSeparated_iff_quasiSeparatedSpace] at H
  refine @quasiSeparatedSpace_of_quasiSeparated _ _ ?_ H ?_
  Â· exact pullback.map _ _ _ _ (ğŸ™ _) _ _ (by simp) (Category.comp_id _) â‰«
      (pullbackRightPullbackFstIso g (Z.affineCover.map i) f).hom
  Â· apply (config := {allowSynthFailures := true}) AlgebraicGeometry.quasiSeparatedOfMono
    -- Porting note: this instance was automatic
    apply mono_comp
#align algebraic_geometry.quasi_separated_of_comp AlgebraicGeometry.quasiSeparatedOfComp

theorem exists_eq_pow_mul_of_isAffineOpen (X : Scheme) (U : Opens X.carrier) (hU : IsAffineOpen U)
    (f : X.presheaf.obj (op U)) (x : X.presheaf.obj (op <| X.basicOpen f)) :
    âˆƒ (n : â„•) (y : X.presheaf.obj (op U)), y |_ X.basicOpen f = (f |_ X.basicOpen f) ^ n * x := by
  have := (isLocalization_basicOpen hU f).2
  obtain âŸ¨âŸ¨y, _, n, rflâŸ©, dâŸ© := this x
  use n, y
  delta TopCat.Presheaf.restrictOpen TopCat.Presheaf.restrict
  simpa [mul_comm x] using d.symm
#align algebraic_geometry.exists_eq_pow_mul_of_is_affine_open AlgebraicGeometry.exists_eq_pow_mul_of_isAffineOpen

set_option maxHeartbeats 500000 in
theorem exists_eq_pow_mul_of_is_compact_of_quasi_separated_space_aux (X : Scheme)
    (S : X.affineOpens) (Uâ‚ Uâ‚‚ : Opens X.carrier) {nâ‚ nâ‚‚ : â„•} {yâ‚ : X.presheaf.obj (op Uâ‚)}
    {yâ‚‚ : X.presheaf.obj (op Uâ‚‚)} {f : X.presheaf.obj (op <| Uâ‚ âŠ” Uâ‚‚)}
    {x : X.presheaf.obj (op <| X.basicOpen f)} (hâ‚ : S.1 â‰¤ Uâ‚) (hâ‚‚ : S.1 â‰¤ Uâ‚‚)
    (eâ‚ :
      X.presheaf.map
          (homOfLE <| X.basicOpen_le (X.presheaf.map (homOfLE le_sup_left).op f) : _ âŸ¶ Uâ‚).op yâ‚ =
        X.presheaf.map (homOfLE (by erw [X.basicOpen_res]; exact inf_le_left)).op
              (X.presheaf.map (homOfLE le_sup_left).op f) ^
            nâ‚ *
          (X.presheaf.map (homOfLE (by erw [X.basicOpen_res]; exact inf_le_right)).op) x)
    (eâ‚‚ :
      X.presheaf.map
          (homOfLE <| X.basicOpen_le (X.presheaf.map (homOfLE le_sup_right).op f) : _ âŸ¶ Uâ‚‚).op yâ‚‚ =
        X.presheaf.map (homOfLE (by rw [X.basicOpen_res]; exact inf_le_left)).op
              (X.presheaf.map (homOfLE le_sup_right).op f) ^
            nâ‚‚ *
          (X.presheaf.map (homOfLE (by rw [X.basicOpen_res]; exact inf_le_right)).op) x) :
    âˆƒ n : â„•,
      X.presheaf.map (homOfLE <| hâ‚).op
          (X.presheaf.map (homOfLE le_sup_left).op f ^ (n + nâ‚‚) * yâ‚) =
        X.presheaf.map (homOfLE <| hâ‚‚).op
          (X.presheaf.map (homOfLE le_sup_right).op f ^ (n + nâ‚) * yâ‚‚) := by
  -- have : IsLocalization.Away _ _ :=
  --   isLocalization_basicOpen S.2 (X.presheaf.map (homOfLE <| le_trans hâ‚ le_sup_left).op f)
  obtain âŸ¨âŸ¨_, n, rflâŸ©, eâŸ© :=
    (@IsLocalization.eq_iff_exists _ _ _ _ _ _
      (isLocalization_basicOpen S.2 (X.presheaf.map (homOfLE <| le_trans hâ‚ le_sup_left).op f))
        (X.presheaf.map (homOfLE <| hâ‚).op
          (X.presheaf.map (homOfLE le_sup_left).op f ^ nâ‚‚ * yâ‚))
        (X.presheaf.map (homOfLE <| hâ‚‚).op
          (X.presheaf.map (homOfLE le_sup_right).op f ^ nâ‚ * yâ‚‚))).mp <| by
    -- Porting note: was just a `simp`, but know as some lemmas need `erw`, just a `simp` does not
    -- leave the goal in a desired form
    rw [RingHom.algebraMap_toAlgebra, map_mul, map_mul, map_pow, map_pow, map_mul, map_pow, map_mul]
    erw [map_pow]
    rw [â†comp_apply, â†comp_apply]
    erw [â†comp_apply, â†comp_apply, â†comp_apply, â†comp_apply]
    simp only [â† Functor.map_comp, â† op_comp, homOfLE_comp]
    have hâ‚ƒ : X.basicOpen ((X.presheaf.map (homOfLE (hâ‚.trans le_sup_left)).op) f) â‰¤ S.val := by
      simpa only [X.basicOpen_res] using inf_le_left
    trans X.presheaf.map (homOfLE <| hâ‚ƒ.trans <| hâ‚.trans le_sup_left).op f ^ (nâ‚‚ + nâ‚) *
      X.presheaf.map (homOfLE <| (X.basicOpen_res f _).trans_le inf_le_right).op x
    Â· rw [pow_add, mul_assoc]; congr 1
      convert congr_arg (X.presheaf.map (homOfLE _).op) eâ‚ using 1
      pick_goal 3
      Â· rw [X.basicOpen_res, X.basicOpen_res]; rintro x âŸ¨Hâ‚, Hâ‚‚âŸ©; exact âŸ¨hâ‚ Hâ‚, Hâ‚‚âŸ©
      Â· simp only [map_pow, map_mul, â† comp_apply, â† Functor.map_comp, â† op_comp]; congr 1
      Â· simp only [map_pow, map_mul, â† comp_apply, â† Functor.map_comp, â† op_comp]; congr
    Â· rw [add_comm, pow_add, mul_assoc]; congr 1
      convert congr_arg (X.presheaf.map (homOfLE _).op) eâ‚‚.symm
      Â· simp only [map_pow, map_mul, â† comp_apply, â† Functor.map_comp, â† op_comp]; congr
      Â· simp only [map_pow, map_mul, â† comp_apply, â† Functor.map_comp, â† op_comp]; congr
      Â· simp only [X.basicOpen_res]
        rintro x âŸ¨Hâ‚, Hâ‚‚âŸ©; exact âŸ¨hâ‚‚ Hâ‚, Hâ‚‚âŸ©
  use n
  simp only [pow_add, map_pow, map_mul, â† comp_apply, â† mul_assoc, â† Functor.map_comp,
    Subtype.coe_mk] at e âŠ¢
  exact e
#align algebraic_geometry.exists_eq_pow_mul_of_is_compact_of_quasi_separated_space_aux AlgebraicGeometry.exists_eq_pow_mul_of_is_compact_of_quasi_separated_space_aux

set_option maxHeartbeats 700000 in
theorem exists_eq_pow_mul_of_isCompact_of_isQuasiSeparated (X : Scheme.{u}) (U : Opens X.carrier)
    (hU : IsCompact U.1) (hU' : IsQuasiSeparated U.1) (f : X.presheaf.obj (op U))
    (x : X.presheaf.obj (op <| X.basicOpen f)) :
    âˆƒ (n : â„•) (y : X.presheaf.obj (op U)), y |_ X.basicOpen f = (f |_ X.basicOpen f) ^ n * x := by
  delta TopCat.Presheaf.restrictOpen TopCat.Presheaf.restrict
  revert hU' f x
  -- Porting note: complains `expected type is not available`, but tell Lean that it is underscore
  -- is sufficient
  apply compact_open_induction_on (P := _) U hU
  Â· intro _ f x
    use 0, f
    refine' @Subsingleton.elim _
      (CommRingCat.subsingleton_of_isTerminal (X.sheaf.isTerminalOfEqEmpty _)) _ _
    erw [eq_bot_iff]
    exact X.basicOpen_le f
  Â· -- Given `f : ğ’ª(S âˆª U), x : ğ’ª(X_f)`, we need to show that `f ^ n * x` is the restriction of
    -- some `y : ğ’ª(S âˆª U)` for some `n : â„•`.
    intro S hS U hU hSU f x
    -- We know that such `yâ‚, nâ‚` exists on `S` by the induction hypothesis.
    obtain âŸ¨nâ‚, yâ‚, hyâ‚âŸ© :=
      hU (hSU.of_subset <| Set.subset_union_left _ _) (X.presheaf.map (homOfLE le_sup_left).op f)
        (X.presheaf.map (homOfLE _).op x)
    -- Â· rw [X.basicOpen_res]; exact inf_le_right
    -- We know that such `yâ‚‚, nâ‚‚` exists on `U` since `U` is affine.
    obtain âŸ¨nâ‚‚, yâ‚‚, hyâ‚‚âŸ© :=
      exists_eq_pow_mul_of_isAffineOpen X _ U.2 (X.presheaf.map (homOfLE le_sup_right).op f)
        (X.presheaf.map (homOfLE _).op x)
    delta TopCat.Presheaf.restrictOpen TopCat.Presheaf.restrict at hyâ‚‚
    -- swap; Â· rw [X.basicOpen_res]; exact inf_le_right
    -- Since `S âˆª U` is quasi-separated, `S âˆ© U` can be covered by finite affine opens.
    obtain âŸ¨s, hs', hsâŸ© :=
      (isCompact_open_iff_eq_finset_affine_union _).mp
        âŸ¨hSU _ _ (Set.subset_union_left _ _) S.2 hS (Set.subset_union_right _ _) U.1.2
            U.2.isCompact,
          (S âŠ“ U.1).2âŸ©
    haveI := hs'.to_subtype
    cases nonempty_fintype s
    replace hs : S âŠ“ U.1 = iSup fun i : s => (i : Opens X.carrier) := by ext1; simpa using hs
    have hsâ‚ : âˆ€ i : s, i.1.1 â‰¤ S := by
      intro i; change (i : Opens X.carrier) â‰¤ S
      refine' le_trans _ inf_le_left; swap; exact U.1; erw [hs]
      -- Porting note: have to add argument explicitly
      exact @le_iSup (Opens X) s _ (fun (i : s) => (i : Opens X)) i
    have hsâ‚‚ : âˆ€ i : s, i.1.1 â‰¤ U.1 := by
      intro i; change (i : Opens X.carrier) â‰¤ U
      refine' le_trans _ inf_le_right; swap; exact S; erw [hs]
      -- Porting note: have to add argument explicitly
      exact @le_iSup (Opens X) s _ (fun (i : s) => (i : Opens X)) i
    -- On each affine open in the intersection, we have `f ^ (n + nâ‚‚) * yâ‚ = f ^ (n + nâ‚) * yâ‚‚`
    -- for some `n` since `f ^ nâ‚‚ * yâ‚ = f ^ (nâ‚ + nâ‚‚) * x = f ^ nâ‚ * yâ‚‚` on `X_f`.
    have :
      âˆ€ i : s,
        âˆƒ n : â„•,
          X.presheaf.map (homOfLE <| hsâ‚ i).op
              (X.presheaf.map (homOfLE le_sup_left).op f ^ (n + nâ‚‚) * yâ‚) =
            X.presheaf.map (homOfLE <| hsâ‚‚ i).op
              (X.presheaf.map (homOfLE le_sup_right).op f ^ (n + nâ‚) * yâ‚‚) := by
      intro i
      exact
        exists_eq_pow_mul_of_is_compact_of_quasi_separated_space_aux X i.1 S U (hsâ‚ i) (hsâ‚‚ i) hyâ‚
          hyâ‚‚
    choose n hn using this
    -- We can thus choose a big enough `n` such that `f ^ (n + nâ‚‚) * yâ‚ = f ^ (n + nâ‚) * yâ‚‚`
    -- on `S âˆ© U`.
    have :
      X.presheaf.map (homOfLE <| inf_le_left).op
          (X.presheaf.map (homOfLE le_sup_left).op f ^ (Finset.univ.sup n + nâ‚‚) * yâ‚) =
        X.presheaf.map (homOfLE <| inf_le_right).op
          (X.presheaf.map (homOfLE le_sup_right).op f ^ (Finset.univ.sup n + nâ‚) * yâ‚‚) := by
      fapply X.sheaf.eq_of_locally_eq' fun i : s => i.1.1
      Â· refine' fun i => homOfLE _; erw [hs];
        -- Porting note: have to add argument explicitly
        exact @le_iSup (Opens X) s _ (fun (i : s) => (i : Opens X)) i
      Â· exact le_of_eq hs
      Â· intro i
        replace hn :=
          congr_arg
            (fun x =>
              X.presheaf.map (homOfLE (le_trans (hsâ‚ i) le_sup_left)).op f ^
                  (Finset.univ.sup n - n i) *
                x)
            (hn i)
        dsimp only at hn
        delta Scheme.sheaf SheafedSpace.sheaf
        simp only [â† map_pow, map_mul, â† comp_apply, â† Functor.map_comp, â† op_comp, â† mul_assoc]
          at hn âŠ¢
        erw [â† map_mul, â† map_mul] at hn
        rw [â† pow_add, â† pow_add, â† add_assoc, â† add_assoc, tsub_add_cancel_of_le] at hn
        convert hn
        exact Finset.le_sup (Finset.mem_univ _)
    use Finset.univ.sup n + nâ‚ + nâ‚‚
    -- By the sheaf condition, since `f ^ (n + nâ‚‚) * yâ‚ = f ^ (n + nâ‚) * yâ‚‚`, it can be glued into
    -- the desired section on `S âˆª U`.
    use (X.sheaf.objSupIsoProdEqLocus S U.1).inv âŸ¨âŸ¨_ * _, _ * _âŸ©, thisâŸ©
    refine' X.sheaf.eq_of_locally_eqâ‚‚
        (homOfLE (_ : X.basicOpen (X.presheaf.map (homOfLE le_sup_left).op f) â‰¤ _))
        (homOfLE (_ : X.basicOpen (X.presheaf.map (homOfLE le_sup_right).op f) â‰¤ _)) _ _ _ _ _
    Â· rw [X.basicOpen_res]; exact inf_le_right
    Â· rw [X.basicOpen_res]; exact inf_le_right
    Â· rw [X.basicOpen_res, X.basicOpen_res]
      erw [â† inf_sup_right]
      refine' le_inf_iff.mpr âŸ¨X.basicOpen_le f, le_of_eq rflâŸ©
    Â· convert congr_arg (X.presheaf.map
        -- Porting note: needs to be explicit here
        (homOfLE (by restrict_tac :
          X.basicOpen (X.presheaf.map (homOfLE (le_sup_left : S â‰¤ S âŠ” U.1)).op f) â‰¤ S)).op)
        (X.sheaf.objSupIsoProdEqLocus_inv_fst S U.1 âŸ¨âŸ¨_ * _, _ * _âŸ©, thisâŸ©) using 1
      Â· delta Scheme.sheaf SheafedSpace.sheaf
        -- Porting note: was just a single `simp only [...]`
        simp only
        erw [â† comp_apply, â† comp_apply, â† comp_apply, â† comp_apply]
        simp only [â† Functor.map_comp, â† op_comp]
        congr 1
      Â· delta Scheme.sheaf SheafedSpace.sheaf
        -- Porting note: was just a single `simp only [...]`
        simp only [map_pow, map_mul]
        erw [â† comp_apply, â† comp_apply]
        simp only [â† Functor.map_comp, â† op_comp, mul_assoc, pow_add]
        erw [hyâ‚]; congr 1; rw [â† mul_assoc, â† mul_assoc]; congr 1
        rw [mul_comm, â† comp_apply, â† Functor.map_comp]; congr 1
    Â· convert
        congr_arg (X.presheaf.map (homOfLE _).op)
          (X.sheaf.objSupIsoProdEqLocus_inv_snd S U.1 âŸ¨âŸ¨_ * _, _ * _âŸ©, thisâŸ©) using
        1
      pick_goal 3
      Â· rw [X.basicOpen_res]; restrict_tac
      Â· delta Scheme.sheaf SheafedSpace.sheaf
        -- Porting note: was just a single `simp only [...]`
        simp only
        erw [â† comp_apply, â† comp_apply, â† comp_apply, â† comp_apply]
        simp only [â† Functor.map_comp, â† op_comp]
        congr 1
      Â· delta Scheme.sheaf SheafedSpace.sheaf
        -- Porting note: was just a single `simp only [...]`
        simp only [map_pow, map_mul]
        erw [â† comp_apply, â† comp_apply]
        simp only [â† Functor.map_comp, â† op_comp, mul_assoc, pow_add]
        erw [hyâ‚‚]; rw [â† comp_apply, â† Functor.map_comp]; congr 1
#align algebraic_geometry.exists_eq_pow_mul_of_is_compact_of_is_quasi_separated AlgebraicGeometry.exists_eq_pow_mul_of_isCompact_of_isQuasiSeparated

/-- If `U` is qcqs, then `Î“(X, D(f)) â‰ƒ Î“(X, U)_f` for every `f : Î“(X, U)`.
This is known as the **Qcqs lemma** in [R. Vakil, *The rising sea*][RisingSea]. -/
theorem is_localization_basicOpen_of_qcqs {X : Scheme} {U : Opens X.carrier} (hU : IsCompact U.1)
    (hU' : IsQuasiSeparated U.1) (f : X.presheaf.obj (op U)) :
    IsLocalization.Away f (X.presheaf.obj (op <| X.basicOpen f)) := by
  constructor
  Â· rintro âŸ¨_, n, rflâŸ©
    simp only [map_pow, Subtype.coe_mk, RingHom.algebraMap_toAlgebra]
    exact IsUnit.pow _ (RingedSpace.isUnit_res_basicOpen _ f)
  Â· intro z
    obtain âŸ¨n, y, eâŸ© := exists_eq_pow_mul_of_isCompact_of_isQuasiSeparated X U hU hU' f z
    refine' âŸ¨âŸ¨y, _, n, rflâŸ©, _âŸ©
    simpa only [map_pow, Subtype.coe_mk, RingHom.algebraMap_toAlgebra, mul_comm z] using e.symm
  Â· intro x y
    rw [â† sub_eq_zero, â† map_sub, RingHom.algebraMap_toAlgebra]
    simp_rw [â† @sub_eq_zero _ _ (_ * x) (_ * y), â† mul_sub]
    generalize x - y = z
    constructor
    Â· intro H
      obtain âŸ¨n, eâŸ© := exists_pow_mul_eq_zero_of_res_basicOpen_eq_zero_of_isCompact X hU _ _ H
      refine' âŸ¨âŸ¨_, n, rflâŸ©, _âŸ©
      simpa [mul_comm z] using e
    Â· rintro âŸ¨âŸ¨_, n, rflâŸ©, e : f ^ n * z = 0âŸ©
      rw [â† ((RingedSpace.isUnit_res_basicOpen _ f).pow n).mul_right_inj, mul_zero, â†
        map_pow]
      -- Porting note: this one lemma needs `erw`
      erw [â† map_mul]
      rw [e, map_zero]
#align algebraic_geometry.is_localization_basic_open_of_qcqs AlgebraicGeometry.is_localization_basicOpen_of_qcqs

end AlgebraicGeometry
