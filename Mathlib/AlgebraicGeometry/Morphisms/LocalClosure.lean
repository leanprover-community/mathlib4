/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.AlgebraicGeometry.Morphisms.Basic

/-!
# Local closure of morphism properties

We define the source local closure of a property `P` wrt. a morphism property `W` and show it
inherits stability properties from `P`.
-/

universe u

open CategoryTheory Limits MorphismProperty

namespace AlgebraicGeometry

variable (W : MorphismProperty Scheme.{u})

/-- The source (Zariski-)local closure of `P` is satisfied if there exists
an open cover of the source on which `P` is satisfied. -/
def sourceLocalClosure (P : MorphismProperty Scheme.{u}) : MorphismProperty Scheme.{u} :=
  fun X _ f â†¦ âˆƒ (ğ’° : Scheme.Cover.{u} W X), âˆ€ (i : ğ’°.J), P (ğ’°.map i â‰« f)

namespace sourceLocalClosure

variable {W} {P Q : MorphismProperty Scheme.{u}} {X Y : Scheme.{u}}

/-- A choice of open cover on which `P` is satisfied if `f` satisfies the source local closure
of `P`. -/
noncomputable def cover {f : X âŸ¶ Y} (hf : sourceLocalClosure W P f) : Scheme.Cover.{u} W X :=
  hf.choose

lemma property_coverMap_comp {f : X âŸ¶ Y} (hf : sourceLocalClosure W P f) (i : hf.cover.J) :
    P (hf.cover.map i â‰« f) :=
  hf.choose_spec i

lemma le [W.ContainsIdentities] [W.RespectsIso] : P â‰¤ sourceLocalClosure W P :=
  fun X Y f hf â†¦ âŸ¨X.coverOfIsIso (ğŸ™ X), by simpaâŸ©

lemma iff_forall_exists [P.RespectsIso] {f : X âŸ¶ Y} :
    sourceLocalClosure IsOpenImmersion P f â†” âˆ€ (x : X), âˆƒ (U : X.Opens), x âˆˆ U âˆ§ P (U.Î¹ â‰« f) := by
  refine âŸ¨fun âŸ¨ğ’°, hfâŸ© x â†¦ ?_, fun H â†¦ ?_âŸ©
  Â· refine âŸ¨(ğ’°.map (ğ’°.f x)).opensRange, ğ’°.covers x, ?_âŸ©
    rw [â† Scheme.Hom.isoOpensRange_inv_comp, Category.assoc, P.cancel_left_of_respectsIso]
    apply hf
  Â· choose U hx hf using H
    exact âŸ¨.mkOfCovers X (fun x â†¦ U x) (fun _ â†¦ (U _).Î¹) (fun x â†¦ âŸ¨x, âŸ¨x, hx xâŸ©, rflâŸ©)
      fun _ â†¦ inferInstance, hfâŸ©

variable [W.IsStableUnderBaseChange] [Scheme.IsJointlySurjectivePreserving W]

instance [P.RespectsLeft Q] [Q.IsStableUnderBaseChange] :
    (sourceLocalClosure W P).RespectsLeft Q := by
  refine âŸ¨fun {X Y} Z f hf g âŸ¨ğ’°, hgâŸ© â†¦ âŸ¨ğ’°.pullbackCover f, fun i â†¦ ?_âŸ©âŸ©
  simpa [pullback.condition_assoc] using
    RespectsLeft.precomp (Q := Q) _ (Q.pullback_snd _ _ hf) _ (hg i)

instance [P.RespectsRight Q] : (sourceLocalClosure W P).RespectsRight Q := by
  refine âŸ¨fun {X Y} Z f hf g âŸ¨ğ’°, hgâŸ© â†¦ âŸ¨ğ’°, fun i â†¦ ?_âŸ©âŸ©
  rw [â† Category.assoc]
  exact RespectsRight.postcomp _ hf _ (hg i)

instance [P.RespectsIso] : (sourceLocalClosure W P).RespectsIso where

instance [P.RespectsIso] [P.RespectsLeft @IsOpenImmersion] :
    IsLocalAtSource (sourceLocalClosure IsOpenImmersion P) where
  iff_of_openCover' {X Y} f ğ’° := by
    refine âŸ¨fun âŸ¨ğ’±, hâŸ© â†¦ fun i â†¦ âŸ¨ğ’±.pullbackCover (ğ’°.map i), fun j â†¦ ?_âŸ©, fun h â†¦ ?_âŸ©
    Â· simpa [pullback.condition_assoc] using
        RespectsLeft.precomp (Q := @IsOpenImmersion) _ inferInstance _ (h j)
    Â· choose ğ’± hğ’± using h
      exact âŸ¨ğ’°.bind ğ’±, fun i â†¦ hğ’± _ _âŸ©

instance [P.IsStableUnderBaseChange] : (sourceLocalClosure W P).IsStableUnderBaseChange := by
  refine .mk' fun X Y S f g _ âŸ¨ğ’°, hgâŸ© â†¦ âŸ¨ğ’°.pullbackCover (pullback.snd f g), fun i â†¦ ?_âŸ©
  simpa [â† pullbackLeftPullbackSndIso_hom_fst, P.cancel_left_of_respectsIso] using
    P.pullback_fst _ _ (hg i)

instance [W.ContainsIdentities] [P.ContainsIdentities] :
    (sourceLocalClosure W P).ContainsIdentities :=
  âŸ¨fun X â†¦ âŸ¨X.coverOfIsIso (ğŸ™ X), fun _ â†¦ P.id_mem _âŸ©âŸ©

instance [W.IsStableUnderComposition] [P.IsStableUnderBaseChange] [P.IsStableUnderComposition] :
    (sourceLocalClosure W P).IsStableUnderComposition := by
  refine âŸ¨fun {X Y Z} f g âŸ¨ğ’°, hfâŸ© âŸ¨ğ’±, hgâŸ© â†¦ ?_âŸ©
  refine âŸ¨ğ’°.bind fun i â†¦ (ğ’±.pullbackCover (ğ’°.map i â‰« f)), fun i â†¦ ?_âŸ©
  simpa [â† pullbackRightPullbackFstIso_inv_snd_fst_assoc, pullback.condition_assoc] using
    P.comp_mem _ _ (P.pullback_snd _ _ (hf _)) (hg i.snd)

instance [W.IsMultiplicative] [P.IsStableUnderBaseChange] [P.IsMultiplicative] :
    (sourceLocalClosure W P).IsMultiplicative where

end sourceLocalClosure

end AlgebraicGeometry
