/-
Copyright (c) 2024 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.UnderlyingMap
import Mathlib.AlgebraicGeometry.Morphisms.SurjectiveOnStalks

/-!

# Preimmersions of schemes

A morphism of schemes `f : X ‚ü∂ Y` is a preimmersion if the underlying map of topological spaces
is an embedding and the induced morphisms of stalks are all surjective. This is not a concept seen
in the literature but it is useful for generalizing results on immersions to other maps including
`Spec ùí™_{X, x} ‚ü∂ X` and inclusions of fibers `Œ∫(x) √ó‚Çì Y ‚ü∂ Y`.

## TODO

* Show preimmersions are local at the target.
* Show preimmersions are stable under pullback.
* Show that `Spec f` is a preimmersion for `f : R ‚ü∂ S` if every `s : S` is of the form `f a / f b`.

-/

universe v u

open CategoryTheory Topology

namespace AlgebraicGeometry

/-- A morphism of schemes `f : X ‚ü∂ Y` is a preimmersion if the underlying map of
topological spaces is an embedding and the induced morphisms of stalks are all surjective. -/
@[mk_iff]
class IsPreimmersion {X Y : Scheme} (f : X ‚ü∂ Y) extends SurjectiveOnStalks f : Prop where
  base_embedding : IsEmbedding f.base

lemma Scheme.Hom.isEmbedding {X Y : Scheme} (f : Hom X Y) [IsPreimmersion f] : IsEmbedding f.base :=
  IsPreimmersion.base_embedding

@[deprecated (since := "2024-10-26")]
alias Scheme.Hom.embedding := Scheme.Hom.isEmbedding

lemma isPreimmersion_eq_inf :
    @IsPreimmersion = (@SurjectiveOnStalks ‚äì topologically IsEmbedding : MorphismProperty _) := by
  ext
  rw [isPreimmersion_iff]
  rfl

/-- Being surjective on stalks is local at the target. -/
instance isSurjectiveOnStalks_isLocalAtTarget : IsLocalAtTarget
    (stalkwise (Function.Surjective ¬∑)) :=
  stalkwiseIsLocalAtTarget_of_respectsIso RingHom.surjective_respectsIso

namespace IsPreimmersion

instance : IsLocalAtTarget @IsPreimmersion :=
  isPreimmersion_eq_inf ‚ñ∏ inferInstance

instance (priority := 900) {X Y : Scheme} (f : X ‚ü∂ Y) [IsOpenImmersion f] : IsPreimmersion f where
  base_embedding := f.isOpenEmbedding.isEmbedding
  surj_on_stalks _ := (ConcreteCategory.bijective_of_isIso (C := CommRingCat) _).2

instance : MorphismProperty.IsMultiplicative @IsPreimmersion where
  id_mem _ := inferInstance
  comp_mem f g _ _ := ‚ü®g.isEmbedding.comp f.isEmbedding‚ü©

instance comp {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsPreimmersion f]
    [IsPreimmersion g] : IsPreimmersion (f ‚â´ g) :=
  MorphismProperty.IsStableUnderComposition.comp_mem f g inferInstance inferInstance

instance (priority := 900) {X Y} (f : X ‚ü∂ Y) [IsPreimmersion f] : Mono f := by
  refine (Scheme.forgetToLocallyRingedSpace ‚ãô
    LocallyRingedSpace.forgetToSheafedSpace).mono_of_mono_map ?_
  apply SheafedSpace.mono_of_base_injective_of_stalk_epi
  ¬∑ exact f.isEmbedding.injective
  ¬∑ exact fun x ‚Ü¶ ConcreteCategory.epi_of_surjective _ (f.stalkMap_surjective x)

theorem of_comp {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsPreimmersion g]
    [IsPreimmersion (f ‚â´ g)] : IsPreimmersion f where
  base_embedding := by
    have h := (f ‚â´ g).isEmbedding
    rwa [‚Üê g.isEmbedding.of_comp_iff]
  surj_on_stalks x := by
    have h := (f ‚â´ g).stalkMap_surjective x
    rw [Scheme.stalkMap_comp] at h
    exact Function.Surjective.of_comp h

theorem comp_iff {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsPreimmersion g] :
    IsPreimmersion (f ‚â´ g) ‚Üî IsPreimmersion f :=
  ‚ü®fun _ ‚Ü¶ of_comp f g, fun _ ‚Ü¶ inferInstance‚ü©

lemma Spec_map_iff {R S : CommRingCat.{u}} (f : R ‚ü∂ S) :
    IsPreimmersion (Spec.map f) ‚Üî IsEmbedding (PrimeSpectrum.comap f.hom) ‚àß
      f.hom.SurjectiveOnStalks := by
  haveI : (RingHom.toMorphismProperty <| fun f ‚Ü¶ Function.Surjective f).RespectsIso := by
    rw [‚Üê RingHom.toMorphismProperty_respectsIso_iff]
    exact RingHom.surjective_respectsIso
  rw [‚Üê HasRingHomProperty.Spec_iff (P := @SurjectiveOnStalks), isPreimmersion_iff, and_comm]
  rfl

lemma mk_Spec_map {R S : CommRingCat.{u}} {f : R ‚ü∂ S}
    (h‚ÇÅ : IsEmbedding (PrimeSpectrum.comap f.hom)) (h‚ÇÇ : f.hom.SurjectiveOnStalks) :
    IsPreimmersion (Spec.map f) :=
  (Spec_map_iff f).mpr ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©

lemma of_isLocalization {R S : Type u} [CommRing R] (M : Submonoid R) [CommRing S]
    [Algebra R S] [IsLocalization M S] :
    IsPreimmersion (Spec.map (CommRingCat.ofHom <| algebraMap R S)) :=
  IsPreimmersion.mk_Spec_map
    (PrimeSpectrum.localization_comap_isEmbedding (R := R) S M)
    (RingHom.surjectiveOnStalks_of_isLocalization (M := M) S)

open Limits MorphismProperty in
instance : IsStableUnderBaseChange @IsPreimmersion := by
  refine .mk' fun X Y Z f g _ _ ‚Ü¶ ?_
  have := pullback_fst (P := @SurjectiveOnStalks) f g inferInstance
  constructor
  let L (x : (pullback f g : _)) : { x : X √ó Y | f.base x.1 = g.base x.2 } :=
    ‚ü®‚ü®(pullback.fst f g).base x, (pullback.snd f g).base x‚ü©,
    by simp only [Set.mem_setOf, ‚Üê Scheme.comp_base_apply, pullback.condition]‚ü©
  have : IsEmbedding L := IsEmbedding.of_comp (by fun_prop) continuous_subtype_val
    (SurjectiveOnStalks.isEmbedding_pullback f g)
  exact IsEmbedding.subtypeVal.comp ((TopCat.pullbackHomeoPreimage _ f.continuous _
    g.isEmbedding).isEmbedding.comp this)

end IsPreimmersion

end AlgebraicGeometry
