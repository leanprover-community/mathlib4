/-
Copyright (c) 2024 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.OpenImmersion
import Mathlib.AlgebraicGeometry.Morphisms.QuasiCompact
import Mathlib.CategoryTheory.MorphismProperty.Composition
import Mathlib.RingTheory.LocalProperties

/-!

# Preimmersions of schemes

A morphism of schemes `f : X ‚ü∂ Y` is a preimmersion if the underlying map of topological spaces
is an embedding and the induced morphisms of stalks are all surjective. This is not a concept seen
in the literature but it is useful for generalizing results on immersions to other maps including
`Spec ùí™_{X, x} ‚ü∂ X` and inclusions of fibers `Œ∫(x) √ó‚Çì Y ‚ü∂ Y`.

## TODO

* Show preimmersions are local at the target.
* Show preimmersions are stable under pullback.
* Show that `Spec f` is a preimmersion for `f : R ‚ü∂ S` if every `s : S` is of the form `f a / f b`.

-/

universe v u

open CategoryTheory

namespace AlgebraicGeometry

/-- A morphism of schemes `f : X ‚ü∂ Y` is a preimmersion if the underlying map of
topological spaces is an embedding and the induced morphisms of stalks are all surjective. -/
class IsPreimmersion {X Y : Scheme} (f : X ‚ü∂ Y) : Prop where
  base_embedding : Embedding f.1.base
  surj_on_stalks : ‚àÄ x, Function.Surjective (PresheafedSpace.stalkMap f.1 x)

lemma Scheme.Hom.embedding {X Y : Scheme} (f : Hom X Y) [IsPreimmersion f] : Embedding f.1.base :=
  IsPreimmersion.base_embedding

lemma Scheme.Hom.stalkMap_surjective {X Y : Scheme} (f : Hom X Y) [IsPreimmersion f] (x) :
    Function.Surjective (PresheafedSpace.stalkMap f.1 x) :=
  IsPreimmersion.surj_on_stalks x

namespace IsPreimmersion

instance (priority := 900) {X Y : Scheme} (f : X ‚ü∂ Y) [IsOpenImmersion f] : IsPreimmersion f where
  base_embedding := f.openEmbedding.toEmbedding
  surj_on_stalks _ := (ConcreteCategory.bijective_of_isIso _).2

instance : MorphismProperty.IsMultiplicative @IsPreimmersion where
  id_mem _ := inferInstance
  comp_mem {X Y Z} f g hf hg := by
    refine ‚ü®hg.base_embedding.comp hf.base_embedding, fun x ‚Ü¶ ?_‚ü©
    erw [PresheafedSpace.stalkMap.comp]
    exact (hf.surj_on_stalks x).comp (hg.surj_on_stalks (f.1.1 x))

instance comp {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsPreimmersion f]
    [IsPreimmersion g] : IsPreimmersion (f ‚â´ g) :=
  MorphismProperty.IsStableUnderComposition.comp_mem f g inferInstance inferInstance

instance respectsIso : MorphismProperty.RespectsIso @IsPreimmersion := by
  constructor <;> intro X Y Z e f hf <;> infer_instance

attribute [local instance] ConcreteCategory.instFunLike in
open _root_.TopCat.Presheaf in
lemma SheafedSpace.hom_stalk_ext {C : Type u} [Category.{v} C] [ConcreteCategory.{v} C]
    [Limits.HasColimits C] [Limits.PreservesFilteredColimits (forget C)]
    [Limits.HasLimits C] [Limits.PreservesLimits (forget C)] [(forget C).ReflectsIsomorphisms]
    {X Y : SheafedSpace C} (f g : X ‚ü∂ Y) (h : f.base = g.base)
    (h' : ‚àÄ x, PresheafedSpace.stalkMap f x =
      Y.presheaf.stalkSpecializes (specializes_of_eq $ by rw [h]) ‚â´ PresheafedSpace.stalkMap g x) :
    f = g := by
  obtain ‚ü®f, fc‚ü© := f
  obtain ‚ü®g, gc‚ü© := g
  obtain rfl : f = g := h
  congr
  ext U s
  apply section_ext X.sheaf
  intro x
  show X.presheaf.germ x _ = X.presheaf.germ x _
  erw [‚Üê PresheafedSpace.stalkMap_germ_apply ‚ü®f, fc‚ü©, ‚Üê PresheafedSpace.stalkMap_germ_apply ‚ü®f, gc‚ü©,
    h']
  simp

lemma SheafedSpace.mono_of_base_injective_of_stalk_epi {C : Type u} [Category.{v} C]
    [ConcreteCategory.{v} C] [Limits.HasColimits C]
    [Limits.PreservesFilteredColimits (forget C)]
    [Limits.HasLimits C] [Limits.PreservesLimits (forget C)] [(forget C).ReflectsIsomorphisms]
    {X Y : SheafedSpace C}
    (f : X ‚ü∂ Y)
    (h‚ÇÅ : Function.Injective f.base)
    (h‚ÇÇ : ‚àÄ x, Epi (PresheafedSpace.stalkMap f x)) : Mono f := by
  constructor
  intro Z ‚ü®g, gc‚ü© ‚ü®h, hc‚ü© e
  obtain rfl : g = h := ConcreteCategory.hom_ext _ _ fun x ‚Ü¶ h‚ÇÅ congr(($e).base x)
  refine SheafedSpace.hom_stalk_ext ‚ü®g, gc‚ü© ‚ü®g, hc‚ü© rfl fun x ‚Ü¶ ?_
  rw [‚Üê cancel_epi (PresheafedSpace.stalkMap f (g x)), TopCat.Presheaf.stalkSpecializes_refl,
    Category.id_comp]
  rw [‚Üê PresheafedSpace.stalkMap.comp ‚ü®g, gc‚ü© f, ‚Üê PresheafedSpace.stalkMap.comp ‚ü®g, hc‚ü© f]
  congr 1

instance (priority := 900) {X Y} (f : X ‚ü∂ Y) [IsPreimmersion f] : Mono f := by
  refine (Scheme.forgetToLocallyRingedSpace ‚ãô
    LocallyRingedSpace.forgetToSheafedSpace).mono_of_mono_map ?_
  apply SheafedSpace.mono_of_base_injective_of_stalk_epi
  ¬∑ exact f.embedding.inj
  ¬∑ exact fun x ‚Ü¶ ConcreteCategory.epi_of_surjective _ (f.stalkMap_surjective x)

theorem of_comp {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsPreimmersion g]
    [IsPreimmersion (f ‚â´ g)] : IsPreimmersion f where
  base_embedding := by
    have h := (f ‚â´ g).embedding
    rwa [‚Üê g.embedding.of_comp_iff]
  surj_on_stalks x := by
    have h := (f ‚â´ g).stalkMap_surjective x
    erw [Scheme.comp_val, PresheafedSpace.stalkMap.comp] at h
    exact Function.Surjective.of_comp h

theorem comp_iff {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsPreimmersion g] :
    IsPreimmersion (f ‚â´ g) ‚Üî IsPreimmersion f :=
  ‚ü®fun _ ‚Ü¶ of_comp f g, fun _ ‚Ü¶ inferInstance‚ü©

end IsPreimmersion

end AlgebraicGeometry
