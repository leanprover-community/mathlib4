/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten, Andrew Yang
-/
module

public import Mathlib.Algebra.Category.Ring.Under.Limits
public import Mathlib.AlgebraicGeometry.Morphisms.Affine
public import Mathlib.AlgebraicGeometry.PullbackCarrier
public import Mathlib.RingTheory.RingHom.FaithfullyFlat

/-!
# Flat morphisms

A morphism of schemes `f : X âŸ¶ Y` is flat if for each affine `U âŠ† Y` and
`V âŠ† f â»Â¹' U`, the induced map `Î“(Y, U) âŸ¶ Î“(X, V)` is flat. This is equivalent to
asking that all stalk maps are flat (see `AlgebraicGeometry.Flat.iff_flat_stalkMap`).

We show that this property is local, and are stable under compositions and base change.

-/

@[expose] public section

noncomputable section

open CategoryTheory CategoryTheory.Limits Opposite TopologicalSpace

universe v u

namespace AlgebraicGeometry

variable {X Y Z : Scheme.{u}} (f : X âŸ¶ Y)

/-- A morphism of schemes `f : X âŸ¶ Y` is flat if for each affine `U âŠ† Y` and
`V âŠ† f â»Â¹' U`, the induced map `Î“(Y, U) âŸ¶ Î“(X, V)` is flat. This is equivalent to
asking that all stalk maps are flat (see `AlgebraicGeometry.Flat.iff_flat_stalkMap`).
-/
@[mk_iff]
class Flat (f : X âŸ¶ Y) : Prop where
  flat_of_affine_subset :
    âˆ€ (U : Y.affineOpens) (V : X.affineOpens) (e : V.1 â‰¤ f â»Â¹áµ U.1), (f.appLE U V e).hom.Flat

namespace Flat

instance : HasRingHomProperty @Flat RingHom.Flat where
  isLocal_ringHomProperty := RingHom.Flat.propertyIsLocal
  eq_affineLocally' := by
    ext X Y f
    rw [flat_iff, affineLocally_iff_affineOpens_le]

instance (priority := 900) [IsOpenImmersion f] : Flat f :=
  HasRingHomProperty.of_isOpenImmersion
    RingHom.Flat.containsIdentities

instance : MorphismProperty.IsStableUnderComposition @Flat :=
  HasRingHomProperty.stableUnderComposition RingHom.Flat.stableUnderComposition

instance comp {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
    [hf : Flat f] [hg : Flat g] : Flat (f â‰« g) :=
  MorphismProperty.comp_mem _ f g hf hg

instance : MorphismProperty.IsMultiplicative @Flat where
  id_mem _ := inferInstance

instance isStableUnderBaseChange : MorphismProperty.IsStableUnderBaseChange @Flat :=
  HasRingHomProperty.isStableUnderBaseChange RingHom.Flat.isStableUnderBaseChange

instance (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [Flat g] : Flat (pullback.fst f g) :=
  MorphismProperty.pullback_fst _ _ inferInstance

instance (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [Flat f] : Flat (pullback.snd f g) :=
  MorphismProperty.pullback_snd _ _ inferInstance

instance (f : X âŸ¶ Y) (V : Y.Opens) [Flat f] : Flat (f âˆ£_ V) :=
  IsZariskiLocalAtTarget.restrict â€¹_â€º V

instance (f : X âŸ¶ Y) (U : X.Opens) (V : Y.Opens) (e) [Flat f] : Flat (f.resLE V U e) := by
  delta Scheme.Hom.resLE; infer_instance

lemma of_stalkMap (H : âˆ€ x, (f.stalkMap x).hom.Flat) : Flat f :=
  HasRingHomProperty.of_stalkMap RingHom.Flat.ofLocalizationPrime H

lemma stalkMap [Flat f] (x : X) : (f.stalkMap x).hom.Flat :=
  HasRingHomProperty.stalkMap (P := @Flat)
    (fun f hf J hJ â†¦ hf.localRingHom J (J.comap f) rfl) â€¹_â€º x

lemma iff_flat_stalkMap : Flat f â†” âˆ€ x, (f.stalkMap x).hom.Flat :=
  âŸ¨fun _ â†¦ stalkMap f, fun H â†¦ of_stalkMap f HâŸ©

instance {X : Scheme.{u}} {Î¹ : Type v} [Small.{u} Î¹] {Y : Î¹ â†’ Scheme.{u}} {f : âˆ€ i, Y i âŸ¶ X}
    [âˆ€ i, Flat (f i)] : Flat (Sigma.desc f) :=
  IsZariskiLocalAtSource.sigmaDesc (fun _ â†¦ inferInstance)

/-- A surjective, quasi-compact, flat morphism is a quotient map. -/
@[stacks 02JY]
lemma isQuotientMap_of_surjective {X Y : Scheme.{u}} (f : X âŸ¶ Y) [Flat f] [QuasiCompact f]
    [Surjective f] : Topology.IsQuotientMap f := by
  rw [Topology.isQuotientMap_iff]
  refine âŸ¨f.surjective, fun s â†¦ âŸ¨fun hs â†¦ hs.preimage f.continuous, fun hs â†¦ ?_âŸ©âŸ©
  wlog hY : âˆƒ R, Y = Spec R
  Â· let ğ’° := Y.affineCover
    rw [ğ’°.isOpenCover_opensRange.isOpen_iff_inter]
    intro i
    rw [Scheme.Hom.coe_opensRange, â† Set.image_preimage_eq_inter_range]
    apply (ğ’°.f i).isOpenEmbedding.isOpenMap
    refine this (f := pullback.fst (ğ’°.f i) f) _ ?_ âŸ¨_, rflâŸ©
    rw [â† Set.preimage_comp, â† TopCat.coe_comp, â† Scheme.Hom.comp_base, pullback.condition,
      Scheme.Hom.comp_base, TopCat.coe_comp, Set.preimage_comp]
    exact hs.preimage (Scheme.Hom.continuous _)
  obtain âŸ¨R, rflâŸ© := hY
  wlog hX : âˆƒ S, X = Spec S
  Â· have _ : CompactSpace X := QuasiCompact.compactSpace_of_compactSpace f
    let ğ’° := X.affineCover.finiteSubcover
    let p : âˆ (fun i : ğ’°.Iâ‚€ â†¦ ğ’°.X i) âŸ¶ X := Sigma.desc (fun i â†¦ ğ’°.f i)
    refine this (f := (âˆ (fun i : ğ’°.Iâ‚€ â†¦ ğ’°.X i)).isoSpec.inv â‰« p â‰« f) _ _ ?_ âŸ¨_, rflâŸ©
    rw [â† Category.assoc, Scheme.Hom.comp_base, TopCat.coe_comp, Set.preimage_comp]
    exact hs.preimage (_ â‰« p).continuous
  obtain âŸ¨S, rflâŸ© := hX
  obtain âŸ¨Ï†, rflâŸ© := Spec.map_surjective f
  refine ((PrimeSpectrum.isQuotientMap_of_generalizingMap ?_ ?_).isOpen_preimage).mp hs
  Â· exact (surjective_iff (Spec.map Ï†)).mp inferInstance
  Â· apply RingHom.Flat.generalizingMap_comap
    rwa [â† HasRingHomProperty.Spec_iff (P := @Flat)]

/-- A flat surjective morphism of schemes is an epimorphism in the category of schemes. -/
@[stacks 02VW]
lemma epi_of_flat_of_surjective (f : X âŸ¶ Y) [Flat f] [Surjective f] : Epi f := by
  apply CategoryTheory.Functor.epi_of_epi_map (Scheme.forgetToLocallyRingedSpace)
  apply CategoryTheory.Functor.epi_of_epi_map (LocallyRingedSpace.forgetToSheafedSpace)
  apply SheafedSpace.epi_of_base_surjective_of_stalk_mono _ â€¹Surjective fâ€º.surj
  intro x
  apply ConcreteCategory.mono_of_injective
  algebraize [(f.stalkMap x).hom]
  have : Module.FaithfullyFlat (Y.presheaf.stalk (f x)) (X.presheaf.stalk x) :=
    @Module.FaithfullyFlat.of_flat_of_isLocalHom _ _ _ _ _ _ _
      (Flat.stalkMap f x) (f.toLRSHom.prop x)
  exact â€¹RingHom.FaithfullyFlat _â€º.injective

lemma flat_and_surjective_iff_faithfullyFlat_of_isAffine [IsAffine X] [IsAffine Y] :
    Flat f âˆ§ Surjective f â†” f.appTop.hom.FaithfullyFlat := by
  rw [RingHom.FaithfullyFlat.iff_flat_and_comap_surjective,
    MorphismProperty.arrow_mk_iso_iff @Surjective (arrowIsoSpecÎ“OfIsAffine f),
    MorphismProperty.arrow_mk_iso_iff @Flat (arrowIsoSpecÎ“OfIsAffine f),
    â† HasRingHomProperty.Spec_iff (P := @Flat), surjective_iff]
  rfl

end Flat

lemma flat_and_surjective_SpecMap_iff {R S : CommRingCat.{u}} (f : R âŸ¶ S) :
    Flat (Spec.map f) âˆ§ Surjective (Spec.map f) â†” f.hom.FaithfullyFlat := by
  rw [HasRingHomProperty.Spec_iff (P := @Flat),
    RingHom.FaithfullyFlat.iff_flat_and_comap_surjective, surjective_iff]
  rfl

section sections

/-!
## Sections of fibered products

Suppose we are given the following cartesian square:
```
Y --g-â†’ X
|       |
iY      iX
â†“       |
T --f-â†’ S
```
Let `Uâ‚›` be an open of `S`, `Uâ‚“` and `Uâ‚œ` be opens of `X` and `T` mapping into `Uâ‚›`.
One could obtain a canonical map `Î“(X, Uâ‚“) âŠ—[Î“(S, Uâ‚›)] Î“(T, Uâ‚œ) âŸ¶ Î“(X Ã—â‚› T, prâ‚ â»Â¹ Uâ‚“ âˆ© prâ‚‚ â»Â¹ Uâ‚œ)`.

We show that this map is
1. bijective when `Uâ‚›`, `Uâ‚œ`, and `Uâ‚“` are all affine.
2. injective when `Uâ‚›`, `Uâ‚œ` are affine, `Uâ‚“` is compact, and `f` is flat.
3. bijective when `Uâ‚›`, `Uâ‚œ` are affine, `Uâ‚“` is qcqs, and `f` is flat.
4. injective when `Uâ‚›` is affine, `Uâ‚œ` is compact, `Uâ‚“` is qcqs, `f` is flat,
  and `Î“(T, Uâ‚œ)` is flat over `Î“(S, Uâ‚›)` (typically true when `S = Spec k`.)
5. bijective when `Uâ‚›` is affine, `Uâ‚œ` and `Uâ‚“` are qcqs, `f` is flat,
  and `Î“(T, Uâ‚œ)` is flat over `Î“(S, Uâ‚›)` (typically true when `S = Spec k`.)

-/

variable {X Y S T : Scheme.{u}} {f : T âŸ¶ S} {g : Y âŸ¶ X} {iX : X âŸ¶ S} {iY : Y âŸ¶ T}
  (H : IsPullback g iY iX f)
  {US : S.Opens} {UT : T.Opens}
  {UX : X.Opens} (hUST : UT â‰¤ f â»Â¹áµ US) (hUSX : UX â‰¤ iX â»Â¹áµ US)
  {UY : Y.Opens} (hUY : UY = g â»Â¹áµ UX âŠ“ iY â»Â¹áµ UT)

/-- The canonical map `Î“(X, Uâ‚“) âŠ—[Î“(S, Uâ‚›)] Î“(T, Uâ‚œ) âŸ¶ Î“(X Ã—â‚› T, prâ‚ â»Â¹ Uâ‚“ âˆ© prâ‚‚ â»Â¹ Uâ‚œ)`.
This is an isomorphism under various circumstances. -/
abbrev pushoutSection : pushout (iX.appLE US UX hUSX) (f.appLE US UT hUST) âŸ¶ Î“(Y, UY) :=
  pushout.desc (g.appLE UX UY (by simp [*])) (iY.appLE UT UY (by simp [*]))
    (by simp only [Scheme.Hom.appLE_comp_appLE, H.w])

lemma isIso_pushoutSection_iff :
    IsIso (pushoutSection H hUST hUSX hUY) â†” IsPushout (iX.appLE US UX hUSX) (f.appLE US UT hUST)
      (g.appLE UX UY (by simp [*])) (iY.appLE UT UY (by simp [*])) :=
  âŸ¨fun _ â†¦ .of_iso (.of_hasPushout _ _) (.refl _) (.refl _) (.refl _)
    (asIso (pushoutSection H hUST hUSX hUY)) (by simp) (by simp) (by simp) (by simp),
    fun h â†¦ inferInstanceAs (IsIso h.isoPushout.inv)âŸ©

attribute [local simp] IsAffineOpen.isoSpec_hom in
attribute [local simpâ† ] Scheme.Hom.resLE_eq_morphismRestrict in
lemma isIso_pushoutSection_of_isAffineOpen (hUS : IsAffineOpen US) (hUT : IsAffineOpen UT)
    (hUX : IsAffineOpen UX) : IsIso (pushoutSection H hUST hUSX hUY) := by
  refine (isIso_pushoutSection_iff ..).mpr (IsPullback.of_map_of_faithful Scheme.Spec ?_).unop
  have : IsAffine _ := hUS
  have : IsAffine _ := hUT
  have : IsAffine _ := hUX
  have hUY' : IsAffineOpen UY :=
    .of_isIso (Scheme.Hom.isPullback_resLE H hUST hUSX hUY).isoPullback.hom
  exact .of_iso (Scheme.Hom.isPullback_resLE H hUST hUSX hUY).flip hUY'.isoSpec hUT.isoSpec
    hUX.isoSpec hUS.isoSpec (by simp) (by simp) (by simp) (by simp)

open TensorProduct in
lemma mono_pushoutSection_of_iSup_eq {Î¹ : Type*} [Finite Î¹] (VX : Î¹ â†’ X.Opens) (hVU : iSup VX = UX)
    (hV : âˆ€ i, IsIso (pushoutSection H hUST (show VX i â‰¤ _ by aesop) rfl))
    (hT : (f.appLE US UT hUST).hom.Flat) :
    Mono (pushoutSection H hUST hUSX hUY) := by
  have (i : _) : VX i â‰¤ iX â»Â¹áµ US := by clear hV; aesop
  classical
  algebraize [(iX.appLE US UX hUSX).hom, (f.appLE US UT hUST).hom]
  let (i : _) := (iX.appLE US (VX i) (by aesop)).hom.toAlgebra
  let Ïˆ : Î“(X, UX) â†’â‚[Î“(S, US)] Î  i, Î“(X, VX i) := Pi.algHom _ _ fun i â†¦
    âŸ¨(X.presheaf.map (homOfLE (hVU â–¸ le_iSup VX i)).op).hom, fun r â†¦ by
      dsimp [RingHom.algebraMap_toAlgebra]
      simp only [â† CommRingCat.comp_apply, Scheme.Hom.appLE_map]âŸ©
  have hÏˆ : Function.Injective Ïˆ := by
    intro s t est
    apply X.IsSheaf.section_ext fun x hx â†¦ ?_
    simp only [â† hVU, Opens.mem_iSup] at hx
    obtain âŸ¨i, hxiâŸ© := hx
    exact âŸ¨_, _, hxi, congr($est i)âŸ©
  let ÏˆY : Î“(Y, UY) â†’+* Î  i, Î“(Y, g â»Â¹áµ VX i âŠ“ iY â»Â¹áµ UT) := Pi.ringHom fun i â†¦
      (Y.presheaf.map (homOfLE (by subst hUY hVU; gcongr; exact le_iSup _ _)).op).hom
  rw [@ConcreteCategory.mono_iff_injective_of_preservesPullback]
  refine .of_comp (f := ÏˆY) ?_
  have hÏˆ' : Function.Injective (Algebra.TensorProduct.map (AlgHom.id Î“(T, UT) Î“(T, UT)) Ïˆ) :=
    Module.Flat.lTensor_preserves_injective_linearMap Ïˆ.toLinearMap hÏˆ
  cases nonempty_fintype Î¹
  let eâ‚ : pushout (iX.appLE US UX hUSX) (f.appLE US UT hUST) â‰…
      .of (Î“(T, UT) âŠ—[Î“(S, US)] Î“(X, UX)) :=
    (CommRingCat.isPushout_tensorProduct _ _ _).flip.isoPushout.symm
  let eâ‚‚ : (Î“(T, UT) âŠ—[Î“(S, US)] Î  i, Î“(X, VX i)) â‰ƒ+* Î  i, Î“(Y, g â»Â¹áµ VX i âŠ“ iY â»Â¹áµ UT) :=
    (Algebra.TensorProduct.piRight _ Î“(S, US) _ _).toRingEquiv.trans (RingEquiv.piCongrRight fun i â†¦
      (CommRingCat.isPushout_tensorProduct _ _ _).flip.isoPushout.commRingCatIsoToRingEquiv.trans
      (asIso (pushoutSection H hUST (show VX i â‰¤ _ by aesop) rfl)).commRingCatIsoToRingEquiv)
  convert (eâ‚‚.injective.comp hÏˆ').comp eâ‚.commRingCatIsoToRingEquiv.injective
  suffices (ÏˆY.comp (pushoutSection H hUST hUSX hUY).hom).comp eâ‚.inv.hom = eâ‚‚.toRingHom.comp
      (Algebra.TensorProduct.map (AlgHom.id Î“(T, UT) Î“(T, UT)) Ïˆ).toRingHom by
    ext1 x; simpa using congr($this (eâ‚.hom x))
  ext1
  Â· have Hâ‚ : eâ‚.inv.hom.comp Algebra.TensorProduct.includeLeftRingHom =
        (pushout.inr (C := CommRingCat) _ _).hom :=
      congr($((CommRingCat.isPushout_tensorProduct _ _ _).flip.inr_isoPushout_hom).hom)
    have Hâ‚‚ (x j) : eâ‚‚ (x âŠ—â‚œ[â†‘Î“(S, US)] 1) j = pushoutSection H hUST (UX := VX j) (by aesop) rfl
        (pushout.inr (C := CommRingCat) _ _ x) := congr(pushoutSection H hUST (UX := VX j) _ rfl
        ($((CommRingCat.isPushout_tensorProduct â†‘Î“(S, US)
          â†‘Î“(T, UT) â†‘Î“(X, VX j)).flip.inr_isoPushout_hom) x))
    simp only [RingHom.comp_assoc, Hâ‚]
    ext x j
    simp [ÏˆY, Hâ‚‚, â† CommRingCat.comp_apply, -CommRingCat.hom_comp, -ConcreteCategory.comp_apply,
      pushoutSection]
  Â· have Hâ‚ : eâ‚.inv.hom.comp Algebra.TensorProduct.includeRight.toRingHom =
        (pushout.inl (C := CommRingCat) _ _).hom :=
      congr($((CommRingCat.isPushout_tensorProduct _ _ _).flip.inl_isoPushout_hom).hom)
    have Hâ‚‚ (x j) : eâ‚‚ (1 âŠ—â‚œ[â†‘Î“(S, US)] x) j = pushoutSection H hUST (UX := VX j) (by aesop) rfl
        (pushout.inl (C := CommRingCat) _ _ (x j)) := congr(pushoutSection H hUST (UX := VX j) _ rfl
        ($((CommRingCat.isPushout_tensorProduct â†‘Î“(S, US)
          â†‘Î“(T, UT) â†‘Î“(X, VX j)).flip.inl_isoPushout_hom) (x j)))
    simp only [RingHom.comp_assoc, Hâ‚]
    ext x j
    simp [ÏˆY, Hâ‚‚, â† CommRingCat.comp_apply, -CommRingCat.hom_comp, -ConcreteCategory.comp_apply,
      pushoutSection, Ïˆ]

lemma isIso_pushoutSection_of_iSup_eq
    {Î¹ : Type u} [Finite Î¹] (VX : Î¹ â†’ X.Opens) (hVU : iSup VX = UX)
    (hV : âˆ€ i, IsIso (pushoutSection H hUST (show VX i â‰¤ _ by aesop) rfl))
    (hV' : âˆ€ i j, Mono (pushoutSection H hUST
      (show VX i âŠ“ VX j â‰¤ _ from inf_le_left.trans (by clear hV; aesop)) rfl))
    (hT : (f.appLE US UT hUST).hom.Flat) :
    IsIso (pushoutSection H hUST hUSX hUY) := by
  classical
  let D := Pairwise.diagram VX
  have h : iSup D.obj = UX := by
    refine le_antisymm (iSup_le_iff.mpr ?_) ?_
    Â· subst hVU; rintro (i | âŸ¨i, jâŸ©); exacts [le_iSup VX _, inf_le_left.trans (le_iSup VX _)]
    Â· subst hVU; exact iSup_le_iff.mpr fun i â†¦ le_iSup D.obj (.single i)
  let câ‚€ : Cocone D := (colimit.cocone _).extend
    (eqToIso (Y := UX) (by simpa [CompleteLattice.colimit_eq_iSup])).hom
  let F := Under.lift _ ((Functor.const _).map (iX.appLE US UX hUSX) â‰«
    ((X.presheaf.mapCone câ‚€.op).Ï€)) â‹™ Under.pushout (f.appLE US UT hUST) â‹™ Under.forget _
  let G : X.Opens â¥¤ Y.Opens :=
    { obj U := g â»Â¹áµ U âŠ“ iY â»Â¹áµ UT, map h := homOfLE (by gcongr; exact h.le) }
  let Î±F : F âŸ¶ D.op â‹™ G.op â‹™ Y.presheaf :=
  { app i := (pushout.congrHom (by simp) rfl).hom â‰«
      pushoutSection H hUST (by grw [â† hUSX, â† h]; exact le_iSup D.obj i.unop) rfl }
  let c : Cone F := (Under.pushout (f.appLE US UT hUST) â‹™ Under.forget _).mapCone
    (Under.liftCone (X.presheaf.mapCone câ‚€.op) _)
  have := CommRingCat.Under.preservesFiniteLimits_of_flat _ hT
  cases nonempty_fintype Î¹
  let hc : IsLimit c :=
    haveI HX := ((TopCat.Presheaf.isSheaf_iff_isSheafPreservesLimitPairwiseIntersections
      _).mp X.IsSheaf VX).preserves (c := câ‚€.op)
    haveI HX := (HX (IsColimit.extendIso _ (colimit.isColimit _)).op).some
    isLimitOfPreserves (Under.pushout _ â‹™ Under.forget _) (Under.isLimitLiftCone _ _ HX)
  let c'â‚€ : Cocone (D â‹™ G) := (colimit.cocone _).extend
    (eqToIso (Y := UY) (by
      simp only [colimit.cocone_x, CompleteLattice.colimit_eq_iSup]
      eta_expand
      dsimp [G]
      rw [â† iSup_inf_eq, â† Scheme.Hom.preimage_iSup, h, hUY])).hom
  let c' : Cone (D.op â‹™ G.op â‹™ Y.presheaf) := Y.presheaf.mapCone c'â‚€.op
  let hc' : IsLimit c' := by
    letI e : D â‹™ G â‰… Pairwise.diagram fun i â†¦ g â»Â¹áµ VX i âŠ“ iY â»Â¹áµ UT :=
      NatIso.ofComponents (fun | .single i => .refl _ | .pair i j => eqToIso (by
        dsimp [D, G]; rw [Scheme.Hom.preimage_inf, inf_inf_distrib_right]))
    haveI HX := ((TopCat.Presheaf.isSheaf_iff_isSheafPreservesLimitPairwiseIntersections _).mp
      Y.IsSheaf (fun i â†¦ g â»Â¹áµ VX i âŠ“ iY â»Â¹áµ UT)).preserves
        (c := ((Cocones.precompose e.inv).obj c'â‚€).op)
    exact (IsLimit.postcomposeHomEquiv (Functor.isoWhiskerRight (NatIso.op e.symm) Y.presheaf) _)
      ((HX ((IsColimit.precomposeInvEquiv e _).symm
        (IsColimit.extendIso _ (colimit.isColimit _))).op).some.ofIsoLimit (Cones.ext (.refl _)))
  have HÎ±Fâ‚‚ (i j : _) : Mono (Î±F.app (.op <| .pair i j)) := by infer_instance
  let fâ‚ : c.pt âŸ¶ c'.pt := hc'.lift ((Cones.postcompose Î±F).obj c)
  let fâ‚‚ : c'.pt âŸ¶ c.pt := hc.lift âŸ¨c'.pt, âŸ¨fun
    | .op (.single i) => c'.Ï€.app _ â‰« inv (Î±F.app (.op <| .single i))
    | .op (.pair i j) => c'.Ï€.app (.op (.single i)) â‰« inv (Î±F.app (.op <| .single i)) â‰«
        F.map (Quiver.Hom.op <| Pairwise.Hom.left i j), by
    rintro âŸ¨iâŸ© âŸ¨jâŸ© f
    obtain âŸ¨i | âŸ¨i, jâŸ© | âŸ¨i, jâŸ© | âŸ¨i, jâŸ©, rflâŸ© :=
      (show Function.Surjective Quiver.Hom.op from Quiver.Hom.opEquiv.surjective) f
    Â· simp [show Pairwise.Hom.id_single i = ğŸ™ (Pairwise.single i) from rfl]
    Â· simp [show Pairwise.Hom.id_pair i j = ğŸ™ (Pairwise.pair i j) from rfl]
    Â· simp
    Â· rw [â† cancel_mono (Î±F.app _)]
      simpa using (c'.w (Quiver.Hom.op <| Pairwise.Hom.left i j)).trans
        (c'.w (Quiver.Hom.op <| Pairwise.Hom.right i j)).symmâŸ©âŸ©
  let e : c.pt â‰… c'.pt := by
    refine âŸ¨fâ‚, fâ‚‚, hc.hom_ext ?_, hc'.hom_ext ?_âŸ©
    Â· rintro âŸ¨i | âŸ¨i, jâŸ©âŸ© <;> simp [fâ‚, fâ‚‚]
    Â· rintro âŸ¨i | âŸ¨i, jâŸ©âŸ©
      Â· simp [fâ‚, fâ‚‚]
      Â· simpa [fâ‚, fâ‚‚] using c'.w (Quiver.Hom.op <| Pairwise.Hom.left i j)
  convert e.isIso_hom using 1
  Â· refine hc'.hom_ext fun i â†¦ ?_
    rw [hc'.fac]
    ext1
    Â· simp [Î±F, c, Under.liftCone, c', câ‚€]
    Â· simp [Î±F, c, c']

lemma mono_pushoutSection_of_isCompact_of_flat_right [Flat f]
    (hUS : IsAffineOpen US) (hUT : IsAffineOpen UT) (hUX : IsCompact (X := X) UX) :
    Mono (pushoutSection H hUST hUSX hUY) := by
  classical
  obtain âŸ¨I, hI, eâŸ© := isCompact_iff_finite_and_eq_biUnion_affineOpens.mp hUX
  have := hI.to_subtype
  exact mono_pushoutSection_of_iSup_eq H hUST hUSX hUY (fun i : I â†¦ i) (by rwa [iSup_subtype,
    eq_comm]) (fun i â†¦ isIso_pushoutSection_of_isAffineOpen _ _ _ _ hUS hUT i.1.2)
    (Flat.flat_of_affine_subset âŸ¨_, hUSâŸ© âŸ¨_, hUTâŸ© _)

lemma mono_pushoutSection_of_isCompact_of_flat_left [Flat iX]
    (hUS : IsAffineOpen US) (hUX : IsAffineOpen UX) (hUT : IsCompact (X := T) UT) :
    Mono (pushoutSection H hUST hUSX hUY) := by
  suffices Mono (pushoutSection H.flip hUSX hUST (hUY.trans (inf_comm _ _))) by
    rw [â† mono_comp_iff_of_isIso (pushoutSymmetry _ _).hom]; convert this; cat_disch
  exact mono_pushoutSection_of_isCompact_of_flat_right _ _ _ _ hUS hUX hUT

lemma isIso_pushoutSection_of_isQuasiSeparated_of_flat_left [Flat f]
    (hUS : IsAffineOpen US) (hUT : IsAffineOpen UT)
    (hUX : IsCompact (X := X) UX) (hUX' : IsQuasiSeparated (Î± := X) UX) :
    IsIso (pushoutSection H hUST hUSX hUY) := by
  classical
  obtain âŸ¨I, hI, eâŸ© := isCompact_iff_finite_and_eq_biUnion_affineOpens.mp hUX
  have hIUX (i : I) : i.1 â‰¤ UX := by rw [e]; intro i; aesop
  have := hI.to_subtype
  exact isIso_pushoutSection_of_iSup_eq H hUST hUSX hUY (fun i : I â†¦ i) (by rwa [iSup_subtype,
    eq_comm]) (fun i â†¦ isIso_pushoutSection_of_isAffineOpen _ _ _ _ hUS hUT i.1.2) (fun i j â†¦
    mono_pushoutSection_of_isCompact_of_flat_right _ _ _ _ hUS hUT (hUX' _ _ (hIUX _) i.1.1.2
    i.1.2.isCompact (hIUX _) j.1.1.2 j.1.2.isCompact))
    (Flat.flat_of_affine_subset âŸ¨_, hUSâŸ© âŸ¨_, hUTâŸ© _)

lemma isIso_pushoutSection_of_isQuasiSeparated_of_flat_right [Flat iX]
    (hUS : IsAffineOpen US) (hUX : IsAffineOpen UX)
    (hUT : IsCompact (X := T) UT) (hUT' : IsQuasiSeparated (Î± := T) UT) :
    IsIso (pushoutSection H hUST hUSX hUY) := by
  suffices IsIso (pushoutSection H.flip hUSX hUST (hUY.trans (inf_comm _ _))) by
    rw [â† isIso_comp_left_iff (pushoutSymmetry _ _).hom]; convert this; cat_disch
  exact isIso_pushoutSection_of_isQuasiSeparated_of_flat_left _ _ _ _ hUS hUX hUT hUT'

lemma mono_pushoutSection_of_isCompact_of_flat_right_of_ringHomFlat [Flat iX]
    (hUS : IsAffineOpen US) (hUT : IsCompact (X := T) UT) (hUT' : IsQuasiSeparated (Î± := T) UT)
    (hUX : IsCompact (X := X) UX) (hf : (f.appLE US UT hUST).hom.Flat) :
    Mono (pushoutSection H hUST hUSX hUY) := by
  obtain âŸ¨I, hI, eâŸ© := isCompact_iff_finite_and_eq_biUnion_affineOpens.mp hUX
  have := hI.to_subtype
  exact mono_pushoutSection_of_iSup_eq _ _ _ _ (fun i : I â†¦ i) (by rwa [iSup_subtype, eq_comm])
    (fun i â†¦ isIso_pushoutSection_of_isQuasiSeparated_of_flat_right _ _ _ _ hUS i.1.2 hUT hUT') hf

lemma mono_pushoutSection_of_isCompact_of_flat_left_of_ringHomFlat [Flat f]
    (hUS : IsAffineOpen US) (hUT : IsCompact (X := T) UT)
    (hUX : IsCompact (X := X) UX) (hUX' : IsQuasiSeparated (Î± := X) UX)
    (hiX : (iX.appLE US UX hUSX).hom.Flat) :
    Mono (pushoutSection H hUST hUSX hUY) := by
  suffices Mono (pushoutSection H.flip hUSX hUST (hUY.trans (inf_comm _ _))) by
    rw [â† mono_comp_iff_of_isIso (pushoutSymmetry _ _).hom]; convert this; cat_disch
  exact mono_pushoutSection_of_isCompact_of_flat_right_of_ringHomFlat _ _ _ _ hUS hUX hUX' hUT hiX

include H in
lemma isIso_pushoutSection_of_isCompact_of_flat_left_of_ringHomFlat [Flat f]
    (hUS : IsAffineOpen US) (hUT : IsCompact (X := T) UT) (hUT' : IsQuasiSeparated (Î± := T) UT)
    (hUX : IsCompact (X := X) UX) (hUX' : IsQuasiSeparated (Î± := X) UX)
    (hiX : (iX.appLE US UX hUSX).hom.Flat) :
    IsIso (pushoutSection H hUST hUSX hUY) := by
  suffices IsIso (pushoutSection H.flip hUSX hUST (hUY.trans (inf_comm _ _))) by
    rw [â† isIso_comp_left_iff (pushoutSymmetry _ _).hom]; convert this; cat_disch
  obtain âŸ¨I, hI, eâŸ© := isCompact_iff_finite_and_eq_biUnion_affineOpens.mp hUT
  have hIUT (i : I) : i.1 â‰¤ UT := by rw [e]; intro i; aesop
  have := hI.to_subtype
  exact isIso_pushoutSection_of_iSup_eq _ _ _ _ (fun i : I â†¦ i) (by rwa [iSup_subtype, eq_comm])
    (fun i â†¦ isIso_pushoutSection_of_isQuasiSeparated_of_flat_right _ _ _ _ hUS i.1.2 hUX hUX')
    (fun i j â†¦ mono_pushoutSection_of_isCompact_of_flat_right_of_ringHomFlat _ _ _ _ hUS hUX hUX'
    (hUT' _ _ (hIUT _) i.1.1.2 i.1.2.isCompact (hIUT _) j.1.1.2 j.1.2.isCompact) hiX) hiX

end sections

end AlgebraicGeometry
