/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten, Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.ClosedImmersion
import Mathlib.AlgebraicGeometry.PullbackCarrier
import Mathlib.CategoryTheory.Limits.Constructions.Over.Basic
import Mathlib.CategoryTheory.Limits.Constructions.Over.Products
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.Equalizer

/-!

# Separated morphisms

A morphism of schemes is separated if its diagonal morphism is a closed immmersion.

## Main definitions
- `AlgebraicGeometry.IsSeparated`: The class of separated morphisms.
- `AlgebraicGeometry.Scheme.IsSeparated`: The class of separated schemes.
- `AlgebraicGeometry.IsSeparated.hasAffineProperty`:
  A morphism is separated iff the preimage of affine opens are separated schemes.
-/


noncomputable section

open CategoryTheory CategoryTheory.Limits Opposite TopologicalSpace

universe u

open scoped AlgebraicGeometry

namespace AlgebraicGeometry

variable {W X Y Z : Scheme.{u}} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)

/-- A morphism is separated if the diagonal map is a closed immersion. -/
@[mk_iff]
class IsSeparated : Prop where
  /-- A morphism is separated if the diagonal map is a closed immersion. -/
  diagonal_isClosedImmersion : IsClosedImmersion (pullback.diagonal f) := by infer_instance

namespace IsSeparated

attribute [instance] diagonal_isClosedImmersion

theorem isSeparated_eq_diagonal_isClosedImmersion :
    @IsSeparated = MorphismProperty.diagonal @IsClosedImmersion := by
  ext
  exact isSeparated_iff _

/-- Monomorphisms are separated. -/
instance (priority := 900) isSeparated_of_mono [Mono f] : IsSeparated f where

instance : MorphismProperty.RespectsIso @IsSeparated := by
  rw [isSeparated_eq_diagonal_isClosedImmersion]
  infer_instance

instance (priority := 900) [IsSeparated f] : QuasiSeparated f where

instance stableUnderComposition : MorphismProperty.IsStableUnderComposition @IsSeparated := by
  rw [isSeparated_eq_diagonal_isClosedImmersion]
  infer_instance

instance [IsSeparated f] [IsSeparated g] : IsSeparated (f â‰« g) :=
  stableUnderComposition.comp_mem f g inferInstance inferInstance

instance : MorphismProperty.IsMultiplicative @IsSeparated where
  id_mem _ := inferInstance

instance isStableUnderBaseChange : MorphismProperty.IsStableUnderBaseChange @IsSeparated := by
  rw [isSeparated_eq_diagonal_isClosedImmersion]
  infer_instance

instance : IsLocalAtTarget @IsSeparated := by
  rw [isSeparated_eq_diagonal_isClosedImmersion]
  infer_instance

instance (R S : CommRingCat.{u}) (f : R âŸ¶ S) : IsSeparated (Spec.map f) := by
  constructor
  letI := f.hom.toAlgebra
  change IsClosedImmersion
    (Limits.pullback.diagonal (Spec.map (CommRingCat.ofHom (algebraMap R S))))
  rw [diagonal_Spec_map, MorphismProperty.cancel_right_of_respectsIso @IsClosedImmersion]
  exact .spec_of_surjective _ fun x â†¦ âŸ¨.tmul R 1 x,
    (Algebra.TensorProduct.lmul'_apply_tmul (R := R) (S := S) 1 x).trans (one_mul x)âŸ©

@[instance 100]
lemma of_isAffineHom [h : IsAffineHom f] : IsSeparated f := by
  wlog hY : IsAffine Y
  Â· rw [IsLocalAtTarget.iff_of_iSup_eq_top (P := @IsSeparated) _
      (iSup_affineOpens_eq_top Y)]
    intro U
    have H : IsAffineHom (f âˆ£_ U) := IsLocalAtTarget.restrict h U
    exact this _ U.2
  have : IsAffine X := HasAffineProperty.iff_of_isAffine.mp h
  rw [MorphismProperty.arrow_mk_iso_iff @IsSeparated (arrowIsoSpecÎ“OfIsAffine f)]
  infer_instance

instance {S T : Scheme.{u}} (f : X âŸ¶ S) (g : Y âŸ¶ S) (i : S âŸ¶ T) [IsSeparated i] :
    IsClosedImmersion (pullback.mapDesc f g i) :=
  MorphismProperty.of_isPullback (pullback_map_diagonal_isPullback f g i)
    inferInstance

/-- Given `f : X âŸ¶ Y` and `g : Y âŸ¶ Z` such that `g` is separated, the induced map
`X âŸ¶ X Ã—[Z] Y` is a closed immersion. -/
instance [IsSeparated g] :
    IsClosedImmersion (pullback.lift (ğŸ™ _) f (Category.id_comp (f â‰« g))) := by
  rw [â† MorphismProperty.cancel_left_of_respectsIso @IsClosedImmersion (pullback.fst f (ğŸ™ Y))]
  rw [â† MorphismProperty.cancel_right_of_respectsIso @IsClosedImmersion _
    (pullback.congrHom rfl (Category.id_comp g)).inv]
  convert (inferInstanceAs <| IsClosedImmersion (pullback.mapDesc f (ğŸ™ _) g)) using 1
  ext : 1 <;> simp [pullback.condition]

end IsSeparated

section of_injective

open Scheme Pullback

variable (ğ’° : Y.OpenCover) (ğ’± : âˆ€ i, (pullback f (ğ’°.map i)).OpenCover)

lemma Scheme.Pullback.diagonalCoverDiagonalRange_eq_top_of_injective
    (hf : Function.Injective f.base) :
    diagonalCoverDiagonalRange f ğ’° ğ’± = âŠ¤ := by
  rw [â† top_le_iff]
  rintro x -
  simp only [diagonalCoverDiagonalRange, openCoverOfBase_J, openCoverOfBase_obj,
    openCoverOfLeftRight_J, Opens.iSup_mk, Opens.carrier_eq_coe, Hom.coe_opensRange, Opens.coe_mk,
    Set.mem_iUnion, Set.mem_range, Sigma.exists]
  have H : (pullback.fst f f).base x = (pullback.snd f f).base x :=
    hf (by rw [â† Scheme.comp_base_apply, â† Scheme.comp_base_apply, pullback.condition])
  let i := ğ’°.f (f.base ((pullback.fst f f).base x))
  obtain âŸ¨y : ğ’°.obj i, hy : (ğ’°.map i).base y = f.base _âŸ© :=
    ğ’°.covers (f.base ((pullback.fst f f).base x))
  obtain âŸ¨z, hzâ‚, hzâ‚‚âŸ© := exists_preimage_pullback _ _ hy.symm
  let j := (ğ’± i).f z
  obtain âŸ¨w : (ğ’± i).obj j, hy : ((ğ’± i).map j).base w = zâŸ© := (ğ’± i).covers z
  refine âŸ¨i, j, ?_âŸ©
  simp_rw [diagonalCover_map]
  change x âˆˆ Set.range _
  dsimp only [diagonalCover, Cover.bind_obj, openCoverOfLeftRight_obj]
  rw [range_map]
  simp [â† H, â† hzâ‚, â† hy]

lemma Scheme.Pullback.range_diagonal_subset_diagonalCoverDiagonalRange :
    Set.range (pullback.diagonal f).base âŠ† diagonalCoverDiagonalRange f ğ’° ğ’± := by
  rintro _ âŸ¨x, rflâŸ©
  simp only [diagonalCoverDiagonalRange, openCoverOfBase_J, openCoverOfBase_obj,
    openCoverOfLeftRight_J, Opens.iSup_mk, Opens.carrier_eq_coe, Hom.coe_opensRange, Opens.coe_mk,
    Set.mem_iUnion, Set.mem_range, Sigma.exists]
  let i := ğ’°.f (f.base x)
  obtain âŸ¨y : ğ’°.obj i, hy : (ğ’°.map i).base y = f.base xâŸ© := ğ’°.covers (f.base x)
  obtain âŸ¨z, hzâ‚, hzâ‚‚âŸ© := exists_preimage_pullback _ _ hy.symm
  let j := (ğ’± i).f z
  obtain âŸ¨w : (ğ’± i).obj j, hy : ((ğ’± i).map j).base w = zâŸ© := (ğ’± i).covers z
  refine âŸ¨i, j, (pullback.diagonal ((ğ’± i).map j â‰« pullback.snd f (ğ’°.map i))).base w, ?_âŸ©
  rw [â† hzâ‚, â† hy, â† Scheme.comp_base_apply, â† Scheme.comp_base_apply]
  dsimp only [diagonalCover, Cover.pullbackHom, Cover.bind_obj, openCoverOfLeftRight_obj]
  rw [â† Scheme.comp_base_apply]
  congr 5
  apply pullback.hom_ext <;> simp

lemma isClosedImmersion_diagonal_restrict_diagonalCoverDiagonalRange
    [âˆ€ i, IsAffine (ğ’°.obj i)] [âˆ€ i j, IsAffine ((ğ’± i).obj j)] :
    IsClosedImmersion (pullback.diagonal f âˆ£_ diagonalCoverDiagonalRange f ğ’° ğ’±) := by
  let U : (Î£ i, (ğ’± i).J) â†’ (diagonalCoverDiagonalRange f ğ’° ğ’±).toScheme.Opens := fun i â†¦
    (diagonalCoverDiagonalRange f ğ’° ğ’±).Î¹ â»Â¹áµ ((diagonalCover f ğ’° ğ’±).map âŸ¨i.1, i.2, i.2âŸ©).opensRange
  have hU (i) : (diagonalCoverDiagonalRange f ğ’° ğ’±).Î¹ ''áµ U i =
      ((diagonalCover f ğ’° ğ’±).map âŸ¨i.1, i.2, i.2âŸ©).opensRange := by
    rw [TopologicalSpace.Opens.functor_obj_map_obj, inf_eq_right, Hom.image_top_eq_opensRange,
      Opens.opensRange_Î¹]
    exact le_iSup (fun i : Î£ i, (ğ’± i).J â†¦ ((diagonalCover f ğ’° ğ’±).map âŸ¨i.1, i.2, i.2âŸ©).opensRange) i
  have hf : iSup U = âŠ¤ := (TopologicalSpace.Opens.map_iSup _ _).symm.trans
    (diagonalCoverDiagonalRange f ğ’° ğ’±).Î¹_preimage_self
  rw [IsLocalAtTarget.iff_of_iSup_eq_top (P := @IsClosedImmersion) _ hf]
  intro i
  rw [MorphismProperty.arrow_mk_iso_iff (P := @IsClosedImmersion) (morphismRestrictRestrict _ _ _),
    MorphismProperty.arrow_mk_iso_iff (P := @IsClosedImmersion) (morphismRestrictEq _ (hU i)),
    MorphismProperty.arrow_mk_iso_iff (P := @IsClosedImmersion) (diagonalRestrictIsoDiagonal ..)]
  infer_instance

@[stacks 0DVA]
lemma isSeparated_of_injective (hf : Function.Injective f.base) :
    IsSeparated f := by
  constructor
  let ğ’° := Y.affineCover
  let ğ’± (i) := (pullback f (ğ’°.map i)).affineCover
  refine IsLocalAtTarget.of_iSup_eq_top (fun i : PUnit.{0} â†¦ âŠ¤) (by simp) fun _ â†¦ ?_
  rw [â† diagonalCoverDiagonalRange_eq_top_of_injective f ğ’° ğ’± hf]
  exact isClosedImmersion_diagonal_restrict_diagonalCoverDiagonalRange f ğ’° ğ’±

end of_injective

lemma IsClosedImmersion.of_comp [IsClosedImmersion (f â‰« g)] [IsSeparated g] :
    IsClosedImmersion f := by
  rw [â† pullback.lift_snd (ğŸ™ _) f (Category.id_comp (f â‰« g))]
  have := MorphismProperty.pullback_snd (P := @IsClosedImmersion) (f â‰« g) g inferInstance
  infer_instance

instance {I J : X.IdealSheafData} (h : I â‰¤ J) : IsClosedImmersion (I.inclusion h) := by
  have : IsClosedImmersion (I.inclusion h â‰« I.subschemeÎ¹) := by
    simp only [Scheme.IdealSheafData.inclusion_subschemeÎ¹]
    infer_instance
  exact .of_comp _ I.subschemeÎ¹

lemma IsSeparated.of_comp [IsSeparated (f â‰« g)] : IsSeparated f := by
  have := IsSeparated.diagonal_isClosedImmersion (f := f â‰« g)
  rw [pullback.diagonal_comp] at this
  exact âŸ¨@IsClosedImmersion.of_comp _ _ _ _ _ this inferInstanceâŸ©

variable {f g} in
lemma IsSeparated.comp_iff [IsSeparated g] : IsSeparated (f â‰« g) â†” IsSeparated f :=
  âŸ¨fun _ â†¦ .of_comp f g, fun _ â†¦ inferInstanceâŸ©

lemma IsAffineHom.of_comp [IsAffineHom (f â‰« g)] [IsSeparated g] :
    IsAffineHom f := by
  rw [â† pullback.lift_snd (ğŸ™ _) f (Category.id_comp (f â‰« g))]
  have := MorphismProperty.pullback_snd (P := @IsAffineHom) (f â‰« g) g inferInstance
  infer_instance

variable {f g} in
lemma IsAffineHom.comp_iff [IsAffineHom g] :
    IsAffineHom (f â‰« g) â†” IsAffineHom f :=
  âŸ¨fun _ â†¦ .of_comp f g, fun _ â†¦ inferInstanceâŸ©

@[stacks 01KM]
instance isClosedImmersion_equalizer_Î¹_left {S : Scheme} {X Y : Over S} [IsSeparated Y.hom]
    (f g : X âŸ¶ Y) : IsClosedImmersion (equalizer.Î¹ f g).left := by
  refine MorphismProperty.of_isPullback
    ((Limits.isPullback_equalizer_prod f g).map (Over.forget _)).flip ?_
  rw [â† MorphismProperty.cancel_right_of_respectsIso @IsClosedImmersion _
    (Over.prodLeftIsoPullback Y Y).hom]
  convert (inferInstanceAs (IsClosedImmersion (pullback.diagonal Y.hom)))
  ext1 <;> simp [â† Over.comp_left]

/--
Suppose `X` is a reduced scheme and that `f g : X âŸ¶ Y` agree over some separated `Y âŸ¶ Z`.
Then `f = g` if `Î¹ â‰« f = Î¹ â‰« g` for some dominant `Î¹`.
-/
lemma ext_of_isDominant_of_isSeparated [IsReduced X] {f g : X âŸ¶ Y}
    (s : Y âŸ¶ Z) [IsSeparated s] (h : f â‰« s = g â‰« s)
    (Î¹ : W âŸ¶ X) [IsDominant Î¹] (hU : Î¹ â‰« f = Î¹ â‰« g) : f = g := by
  let X' : Over Z := Over.mk (f â‰« s)
  let Y' : Over Z := Over.mk s
  let U' : Over Z := Over.mk (Î¹ â‰« f â‰« s)
  let f' : X' âŸ¶ Y' := Over.homMk f
  let g' : X' âŸ¶ Y' := Over.homMk g
  let Î¹' : U' âŸ¶ X' := Over.homMk Î¹
  have : IsSeparated Y'.hom := â€¹_â€º
  have : IsDominant (equalizer.Î¹ f' g').left := by
    apply (config := { allowSynthFailures := true }) IsDominant.of_comp (equalizer.lift Î¹' ?_).left
    Â· rwa [â† Over.comp_left, equalizer.lift_Î¹]
    Â· ext1; exact hU
  have : Surjective (equalizer.Î¹ f' g').left :=
    surjective_of_isDominant_of_isClosed_range _ IsClosedImmersion.base_closed.2
  have := isIso_of_isClosedImmersion_of_surjective (Y := X) (equalizer.Î¹ f' g').left
  rw [â† cancel_epi (equalizer.Î¹ f' g').left]
  exact congr($(equalizer.condition f' g').left)

variable (S) in
/--
Suppose `X` is a reduced `S`-scheme and `Y` is a separated `S`-scheme.
For any `S`-morphisms `f g : X âŸ¶ Y`, `f = g` if `Î¹ â‰« f = Î¹ â‰« g` for some dominant `Î¹`.
-/
lemma ext_of_isDominant_of_isSeparated' [X.Over S] [Y.Over S] [IsReduced X] [IsSeparated (Y â†˜ S)]
    {f g : X âŸ¶ Y} [f.IsOver S] [g.IsOver S] {W} (Î¹ : W âŸ¶ X) [IsDominant Î¹]
    (hU : Î¹ â‰« f = Î¹ â‰« g) : f = g :=
  ext_of_isDominant_of_isSeparated (Y â†˜ S) (by simp) Î¹ hU

namespace Scheme

/-- A scheme `X` is separated if it is separated over `âŠ¤_ Scheme`. -/
@[mk_iff]
protected class IsSeparated (X : Scheme.{u}) : Prop where
  isSeparated_terminal_from : IsSeparated (terminal.from X)

attribute [instance] IsSeparated.isSeparated_terminal_from

lemma isSeparated_iff_isClosedImmersion_prod_lift {X : Scheme.{u}} :
    X.IsSeparated â†” IsClosedImmersion (prod.lift (ğŸ™ X) (ğŸ™ X)) := by
  rw [isSeparated_iff, AlgebraicGeometry.isSeparated_iff, iff_iff_eq,
    â† MorphismProperty.cancel_right_of_respectsIso @IsClosedImmersion _ (prodIsoPullback X X).hom]
  congr
  ext : 1 <;> simp

instance [X.IsSeparated] : IsClosedImmersion (prod.lift (ğŸ™ X) (ğŸ™ X)) := by
  rwa [â† isSeparated_iff_isClosedImmersion_prod_lift]

instance (priority := 900) {X : Scheme.{u}} [IsAffine X] : X.IsSeparated := âŸ¨inferInstanceâŸ©

instance (priority := 900) [X.IsSeparated] : IsSeparated f := by
  apply (config := { allowSynthFailures := true }) @IsSeparated.of_comp (g := terminal.from Y)
  rw [terminal.comp_from]
  infer_instance

instance (f g : X âŸ¶ Y) [Y.IsSeparated] : IsClosedImmersion (Limits.equalizer.Î¹ f g) :=
  MorphismProperty.of_isPullback (isPullback_equalizer_prod f g).flip inferInstance

end Scheme

instance IsSeparated.hasAffineProperty :
    HasAffineProperty @IsSeparated fun X _ _ _ â†¦ X.IsSeparated := by
  convert HasAffineProperty.of_isLocalAtTarget @IsSeparated with X Y f hY
  rw [Scheme.isSeparated_iff, â† terminal.comp_from f, IsSeparated.comp_iff]
  rfl

/--
Suppose `f g : X âŸ¶ Y` where `X` is a reduced scheme and `Y` is a separated scheme.
Then `f = g` if `Î¹ â‰« f = Î¹ â‰« g` for some dominant `Î¹`.

Also see `ext_of_isDominant_of_isSeparated` for the general version over arbitrary bases.
-/
lemma ext_of_isDominant [IsReduced X] {f g : X âŸ¶ Y} [Y.IsSeparated]
    (Î¹ : W âŸ¶ X) [IsDominant Î¹] (hU : Î¹ â‰« f = Î¹ â‰« g) : f = g :=
  ext_of_isDominant_of_isSeparated (Limits.terminal.from _) (Limits.terminal.hom_ext _ _) Î¹ hU

end AlgebraicGeometry
