/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.Basic
import Mathlib.RingTheory.RingHomProperties

/-!

# Constructors for properties of morphisms between schemes

This file provides some constructors to obtain morphism properties of schemes from other morphism
properties:

- `AffineTargetMorphismProperty.diagonal` : Given an affine target morphism property `P`,
  `P.diagonal f` holds if `P (pullback.mapDesc fâ‚ fâ‚‚ f)` holds for two affine open
  immersions `fâ‚` and `fâ‚‚`.
- `AffineTargetMorphismProperty.of`: Given a morphism property `P` of schemes,
  this is the restriction of `P` to morphisms with affine target. If `P` is local at the
  target, we have `(toAffineTargetMorphismProperty P).targetAffineLocally = P`
  (see `MorphismProperty.targetAffineLocally_toAffineTargetMorphismProperty_eq_of_isLocalAtTarget`).
- `MorphismProperty.topologically`: Given a property `P` of maps of topological spaces,
  `(topologically P) f` holds if `P` holds for the underlying continuous map of `f`.
- `MorphismProperty.stalkwise`: Given a property `P` of ring homs,
  `(stalkwise P) f` holds if `P` holds for all stalk maps.

Also provides API for showing the standard locality and stability properties for these
types of properties.

-/

universe u

open TopologicalSpace CategoryTheory CategoryTheory.Limits Opposite

noncomputable section

namespace AlgebraicGeometry

section Diagonal

/-- The `AffineTargetMorphismProperty` associated to `(targetAffineLocally P).diagonal`.
See `diagonal_targetAffineLocally_eq_targetAffineLocally`.
-/
def AffineTargetMorphismProperty.diagonal (P : AffineTargetMorphismProperty) :
    AffineTargetMorphismProperty :=
  fun {X _} f _ =>
    âˆ€ {Uâ‚ Uâ‚‚ : Scheme} (fâ‚ : Uâ‚ âŸ¶ X) (fâ‚‚ : Uâ‚‚ âŸ¶ X) [IsAffine Uâ‚] [IsAffine Uâ‚‚] [IsOpenImmersion fâ‚]
      [IsOpenImmersion fâ‚‚], P (pullback.mapDesc fâ‚ fâ‚‚ f)
#align algebraic_geometry.affine_target_morphism_property.diagonal AlgebraicGeometry.AffineTargetMorphismProperty.diagonal

instance AffineTargetMorphismProperty.diagonal_respectsIso (P : AffineTargetMorphismProperty)
    [P.toProperty.RespectsIso] : P.diagonal.toProperty.RespectsIso := by
  delta AffineTargetMorphismProperty.diagonal
  apply AffineTargetMorphismProperty.respectsIso_mk
  Â· introv H _ _
    rw [pullback.mapDesc_comp, P.cancel_left_of_respectsIso, P.cancel_right_of_respectsIso]
    -- Porting note: add the following two instances
    have i1 : IsOpenImmersion (fâ‚ â‰« e.hom) := PresheafedSpace.IsOpenImmersion.comp _ _
    have i2 : IsOpenImmersion (fâ‚‚ â‰« e.hom) := PresheafedSpace.IsOpenImmersion.comp _ _
    apply H
  Â· introv H _ _
    -- Porting note: add the following two instances
    have _ : IsAffine Z := isAffine_of_isIso e.inv
    rw [pullback.mapDesc_comp, P.cancel_right_of_respectsIso]
    apply H
#align algebraic_geometry.affine_target_morphism_property.diagonal_respects_iso AlgebraicGeometry.AffineTargetMorphismProperty.diagonal_respectsIso

theorem diagonal_targetAffineLocally_of_openCover
    (P : AffineTargetMorphismProperty) (hP : P.IsLocal)
    {X Y : Scheme.{u}} (f : X âŸ¶ Y) (ğ’° : Scheme.OpenCover.{u} Y) [âˆ€ i, IsAffine (ğ’°.obj i)]
    (ğ’°' : âˆ€ i, Scheme.OpenCover.{u} (pullback f (ğ’°.map i))) [âˆ€ i j, IsAffine ((ğ’°' i).obj j)]
    (hğ’°' : âˆ€ i j k, P (pullback.mapDesc ((ğ’°' i).map j) ((ğ’°' i).map k) pullback.snd)) :
    (targetAffineLocally P).diagonal f := by
  have := hP.1
  let ğ’± := (Scheme.Pullback.openCoverOfBase ğ’° f f).bind fun i =>
    Scheme.Pullback.openCoverOfLeftRight.{u} (ğ’°' i) (ğ’°' i) pullback.snd pullback.snd
  have i1 : âˆ€ i, IsAffine (ğ’±.obj i) := fun i => by dsimp [ğ’±]; infer_instance
  apply (hP.affine_openCover_iff _ ğ’±).mpr
  rintro âŸ¨i, j, kâŸ©
  dsimp [ğ’±]
  convert (P.cancel_left_of_respectsIso
    (pullbackDiagonalMapIso _ _ ((ğ’°' i).map j) ((ğ’°' i).map k)).inv pullback.snd).mp _
  pick_goal 3
  Â· convert hğ’°' i j k; apply pullback.hom_ext <;> simp
  all_goals apply pullback.hom_ext <;>
  simp only [Category.assoc, pullback.lift_fst, pullback.lift_snd, pullback.lift_fst_assoc,
    pullback.lift_snd_assoc]
#align algebraic_geometry.diagonal_target_affine_locally_of_open_cover AlgebraicGeometry.diagonal_targetAffineLocally_of_openCover

theorem AffineTargetMorphismProperty.diagonal_of_targetAffineLocally
    (P : AffineTargetMorphismProperty) (hP : P.IsLocal) {X Y U : Scheme.{u}} (f : X âŸ¶ Y) (g : U âŸ¶ Y)
    [IsAffine U] [IsOpenImmersion g] (H : (targetAffineLocally P).diagonal f) :
    P.diagonal (pullback.snd : pullback f g âŸ¶ _) := by
  have := hP.1
  rintro U V fâ‚ fâ‚‚ hU hV hfâ‚ hfâ‚‚
  replace H := ((hP.affine_openCover_TFAE (pullback.diagonal f)).out 0 3).mp H
  let gâ‚ := pullback.map (fâ‚ â‰« pullback.snd) (fâ‚‚ â‰« pullback.snd) f f
    (fâ‚ â‰« pullback.fst) (fâ‚‚ â‰« pullback.fst) g
    (by rw [Category.assoc, Category.assoc, pullback.condition])
    (by rw [Category.assoc, Category.assoc, pullback.condition])
  specialize H gâ‚
  rw [â† P.cancel_left_of_respectsIso (pullbackDiagonalMapIso f _ fâ‚ fâ‚‚).hom]
  convert H
  apply pullback.hom_ext <;>
    simp only [Category.assoc, pullback.lift_fst, pullback.lift_snd, pullback.lift_fst_assoc,
      pullback.lift_snd_assoc, Category.comp_id, pullbackDiagonalMapIso_hom_fst,
      pullbackDiagonalMapIso_hom_snd]
#align algebraic_geometry.affine_target_morphism_property.diagonal_of_target_affine_locally AlgebraicGeometry.AffineTargetMorphismProperty.diagonal_of_targetAffineLocally

open List in
theorem AffineTargetMorphismProperty.IsLocal.diagonal_affine_openCover_TFAE
    {P : AffineTargetMorphismProperty} (hP : P.IsLocal) {X Y : Scheme.{u}} (f : X âŸ¶ Y) :
    TFAE
      [(targetAffineLocally P).diagonal f,
        âˆƒ (ğ’° : Scheme.OpenCover.{u} Y) (_ : âˆ€ i, IsAffine (ğ’°.obj i)),
          âˆ€ i : ğ’°.J, P.diagonal (pullback.snd : pullback f (ğ’°.map i) âŸ¶ _),
        âˆ€ (ğ’° : Scheme.OpenCover.{u} Y) [âˆ€ i, IsAffine (ğ’°.obj i)] (i : ğ’°.J),
          P.diagonal (pullback.snd : pullback f (ğ’°.map i) âŸ¶ _),
        âˆ€ {U : Scheme} (g : U âŸ¶ Y) [IsAffine U] [IsOpenImmersion g],
          P.diagonal (pullback.snd : pullback f g âŸ¶ _),
        âˆƒ (ğ’° : Scheme.OpenCover.{u} Y) (_ : âˆ€ i, IsAffine (ğ’°.obj i)) (ğ’°' :
          âˆ€ i, Scheme.OpenCover.{u} (pullback f (ğ’°.map i))) (_ : âˆ€ i j, IsAffine ((ğ’°' i).obj j)),
          âˆ€ i j k, P (pullback.mapDesc ((ğ’°' i).map j) ((ğ’°' i).map k) pullback.snd)] := by
  tfae_have 1 â†’ 4
  Â· introv H hU hg _ _; apply P.diagonal_of_targetAffineLocally <;> assumption
  tfae_have 4 â†’ 3
  Â· introv H hğ’°; apply H
  tfae_have 3 â†’ 2
  Â· exact fun H => âŸ¨Y.affineCover, inferInstance, H Y.affineCoverâŸ©
  tfae_have 2 â†’ 5
  Â· rintro âŸ¨ğ’°, hğ’°, HâŸ©
    refine âŸ¨ğ’°, inferInstance, fun _ => Scheme.affineCover _, inferInstance, ?_âŸ©
    intro i j k
    apply H
  tfae_have 5 â†’ 1
  Â· rintro âŸ¨ğ’°, _, ğ’°', _, HâŸ©
    exact diagonal_targetAffineLocally_of_openCover P hP f ğ’° ğ’°' H
  tfae_finish
#align algebraic_geometry.affine_target_morphism_property.is_local.diagonal_affine_open_cover_tfae AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal.diagonal_affine_openCover_TFAE

theorem AffineTargetMorphismProperty.IsLocal.diagonal {P : AffineTargetMorphismProperty}
    (hP : P.IsLocal) : P.diagonal.IsLocal :=
  have := hP.1
  AffineTargetMorphismProperty.isLocalOfOpenCoverImply P.diagonal
    fun {_ _} f => ((hP.diagonal_affine_openCover_TFAE f).out 1 3).mp
#align algebraic_geometry.affine_target_morphism_property.is_local.diagonal AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal.diagonal

theorem diagonal_targetAffineLocally_eq_targetAffineLocally (P : AffineTargetMorphismProperty)
    (hP : P.IsLocal) : (targetAffineLocally P).diagonal = targetAffineLocally P.diagonal := by
  ext _ _ f
  exact ((hP.diagonal_affine_openCover_TFAE f).out 0 1).trans
    ((hP.diagonal.affine_openCover_TFAE f).out 1 0)
#align algebraic_geometry.diagonal_target_affine_locally_eq_target_affine_locally AlgebraicGeometry.diagonal_targetAffineLocally_eq_targetAffineLocally

end Diagonal

section Universally

theorem universally_isLocalAtTarget (P : MorphismProperty Scheme)
    (hP : âˆ€ {X Y : Scheme.{u}} (f : X âŸ¶ Y) (ğ’° : Scheme.OpenCover.{u} Y),
      (âˆ€ i : ğ’°.J, P (pullback.snd : (ğ’°.pullbackCover f).obj i âŸ¶ ğ’°.obj i)) â†’ P f) :
    PropertyIsLocalAtTarget P.universally := by
  refine âŸ¨P.universally_respectsIso, fun {X Y} f U =>
    P.universally_stableUnderBaseChange (isPullback_morphismRestrict f U).flip, ?_âŸ©
  intro X Y f ğ’° h X' Y' iâ‚ iâ‚‚ f' H
  apply hP _ (ğ’°.pullbackCover iâ‚‚)
  intro i
  dsimp
  apply h i (pullback.lift (pullback.fst â‰« iâ‚) (pullback.snd â‰« pullback.snd) _) pullback.snd
  swap
  Â· rw [Category.assoc, Category.assoc, â† pullback.condition, â† pullback.condition_assoc, H.w]
  refine (IsPullback.of_right ?_ (pullback.lift_snd _ _ _) (IsPullback.of_hasPullback _ _)).flip
  rw [pullback.lift_fst, â† pullback.condition]
  exact (IsPullback.of_hasPullback _ _).paste_horiz H.flip
#align algebraic_geometry.universally_is_local_at_target AlgebraicGeometry.universally_isLocalAtTarget

theorem universally_isLocalAtTarget_of_morphismRestrict (P : MorphismProperty Scheme)
    [P.RespectsIso]
    (hPâ‚‚ : âˆ€ {X Y : Scheme.{u}} (f : X âŸ¶ Y) {Î¹ : Type u} (U : Î¹ â†’ Opens Y.carrier)
      (_ : iSup U = âŠ¤), (âˆ€ i, P (f âˆ£_ U i)) â†’ P f) : PropertyIsLocalAtTarget P.universally :=
  universally_isLocalAtTarget P (fun f ğ’° hğ’° => by
    apply hPâ‚‚ f (fun i : ğ’°.J => Scheme.Hom.opensRange (ğ’°.map i)) ğ’°.iSup_opensRange
    simp_rw [P.arrow_mk_iso_iff (morphismRestrictOpensRange f _)]
    exact hğ’°)
#align algebraic_geometry.universally_is_local_at_target_of_morphism_restrict AlgebraicGeometry.universally_isLocalAtTarget_of_morphismRestrict

end Universally

namespace MorphismProperty

section Topologically

/-- `topologically P` holds for a morphism if the underlying topological map satisfies `P`. -/
def topologically
    (P : âˆ€ {Î± Î² : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] (_ : Î± â†’ Î²), Prop) :
    MorphismProperty Scheme.{u} := fun _ _ f => P f.1.base
#align algebraic_geometry.morphism_property.topologically AlgebraicGeometry.MorphismProperty.topologically

variable (P : âˆ€ {Î± Î² : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] (_ : Î± â†’ Î²), Prop)

/-- If a property of maps of topological spaces is stable under composition, the induced
morphism property of schemes is stable under composition. -/
lemma topologically_isStableUnderComposition
    (hP : âˆ€ {Î± Î² Î³ : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³]
      (f : Î± â†’ Î²) (g : Î² â†’ Î³) (_ : P f) (_ : P g), P (g âˆ˜ f)) :
    (MorphismProperty.topologically P).IsStableUnderComposition where
  comp_mem {X Y Z} f g hf hg := by
    simp only [MorphismProperty.topologically, Scheme.comp_coeBase, TopCat.coe_comp]
    exact hP _ _ hf hg

/-- If a property of maps of topological spaces is satisfied by all homeomorphisms,
every isomorphism of schemes satisfies the induced property. -/
lemma topologically_iso_le
    (hP : âˆ€ {Î± Î² : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] (f : Î± â‰ƒâ‚œ Î²), P f) :
    MorphismProperty.isomorphisms Scheme â‰¤ (MorphismProperty.topologically P) := by
  intro X Y e (he : IsIso e)
  have : IsIso e := he
  exact hP (TopCat.homeoOfIso (asIso e.val.base))

/-- If a property of maps of topological spaces is satisfied by homeomorphisms and is stable
under composition, the induced property on schemes respects isomorphisms. -/
lemma topologically_respectsIso
    (hPâ‚ : âˆ€ {Î± Î² : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] (f : Î± â‰ƒâ‚œ Î²), P f)
    (hPâ‚‚ : âˆ€ {Î± Î² Î³ : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³]
      (f : Î± â†’ Î²) (g : Î² â†’ Î³) (_ : P f) (_ : P g), P (g âˆ˜ f)) :
      (MorphismProperty.topologically P).RespectsIso :=
  have : (MorphismProperty.topologically P).IsStableUnderComposition :=
    topologically_isStableUnderComposition P hPâ‚‚
  MorphismProperty.respectsIso_of_isStableUnderComposition (topologically_iso_le P hPâ‚)

/-- To check that a topologically defined morphism property is local at the target,
we may check the corresponding properties on topological spaces. -/
lemma topologically_propertyIsLocalAtTarget
    [(MorphismProperty.topologically P).RespectsIso]
    (hPâ‚‚ : âˆ€ {Î± Î² : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] (f : Î± â†’ Î²) (s : Set Î²),
      P f â†’ P (s.restrictPreimage f))
    (hPâ‚ƒ : âˆ€ {Î± Î² : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] (f : Î± â†’ Î²) {Î¹ : Type u}
      (U : Î¹ â†’ TopologicalSpace.Opens Î²) (_ : iSup U = âŠ¤) (_ : Continuous f),
      (âˆ€ i, P ((U i).carrier.restrictPreimage f)) â†’ P f) :
    PropertyIsLocalAtTarget (MorphismProperty.topologically P) := by
  apply propertyIsLocalAtTarget_of_morphismRestrict
  Â· intro X Y f U hf
    simp_rw [MorphismProperty.topologically, morphismRestrict_val_base]
    exact hPâ‚‚ f.val.base U.carrier hf
  Â· intro X Y f Î¹ U hU hf
    apply hPâ‚ƒ f.val.base U hU
    Â· exact f.val.base.continuous
    Â· intro i
      rw [â† morphismRestrict_val_base]
      exact hf i

end Topologically

/-- `stalkwise P` holds for a morphism if all stalks satisfy `P`. -/
def stalkwise (P : âˆ€ {R S : Type u} [CommRing R] [CommRing S], (R â†’+* S) â†’ Prop) :
    MorphismProperty Scheme.{u} :=
  fun _ _ f => âˆ€ x, P (PresheafedSpace.stalkMap f.val x)

section Stalkwise

variable {P : âˆ€ {R S : Type u} [CommRing R] [CommRing S], (R â†’+* S) â†’ Prop}

/-- If `P` respects isos, then `stalkwise P` respects isos. -/
lemma stalkwise_respectsIso (hP : RingHom.RespectsIso P) :
    (MorphismProperty.stalkwise P).RespectsIso where
  precomp {X Y Z} e f hf := by
    simp only [stalkwise, Scheme.comp_coeBase, TopCat.coe_comp, Function.comp_apply]
    intro x
    erw [PresheafedSpace.stalkMap.comp]
    exact (RingHom.RespectsIso.cancel_right_isIso hP _ _).mpr <| hf (e.hom.val.base x)
  postcomp {X Y Z} e f hf := by
    simp only [stalkwise, Scheme.comp_coeBase, TopCat.coe_comp, Function.comp_apply]
    intro x
    erw [PresheafedSpace.stalkMap.comp]
    exact (RingHom.RespectsIso.cancel_left_isIso hP _ _).mpr <| hf x

/-- If `P` respects isos, then `stalkwise P` is local at the target. -/
lemma stalkwiseIsLocalAtTarget_of_respectsIso (hP : RingHom.RespectsIso P) :
    PropertyIsLocalAtTarget (MorphismProperty.stalkwise P) := by
  have hP' : (RingHom.toMorphismProperty P).RespectsIso :=
    RingHom.toMorphismProperty_respectsIso_iff.mp hP
  letI := stalkwise_respectsIso hP
  apply propertyIsLocalAtTarget_of_morphismRestrict
  Â· intro X Y f U hf x
    apply ((RingHom.toMorphismProperty P).arrow_mk_iso_iff <|
      morphismRestrictStalkMap f U x).mpr <| hf _
  Â· intro X Y f Î¹ U hU hf x
    have hy : f.val.base x âˆˆ iSup U := by rw [hU]; trivial
    obtain âŸ¨i, hiâŸ© := Opens.mem_iSup.mp hy
    exact ((RingHom.toMorphismProperty P).arrow_mk_iso_iff <|
      morphismRestrictStalkMap f (U i) âŸ¨x, hiâŸ©).mp <| hf i âŸ¨x, hiâŸ©

end Stalkwise

end MorphismProperty

section Restriction

/-- If `P` is a property of scheme morphisms, we may restrict `P` to morphisms with affine target
to obtain an `AffineTargetMorphismProperty`. -/
def AffineTargetMorphismProperty.of (P : MorphismProperty Scheme) :
    AffineTargetMorphismProperty := fun _ _ f â†¦ P f

namespace AffineTargetMorphismProperty

/-- Restricting a local at the target morphism property of schemes `P` to morphisms with affine
target and extending to a global property with `targetAffineLocally` yields `P` again,
if `P` is local at the target. -/
lemma targetAffineLocally_of_eq_of_isLocalAtTarget
    (P : MorphismProperty Scheme) (hP : PropertyIsLocalAtTarget P) :
    targetAffineLocally (of P) = P := by
  ext X Y f
  constructor
  Â· intro hf
    simp only [targetAffineLocally, Subtype.forall] at hf
    let ğ’° : Y.OpenCover := Y.affineCover
    apply hP.of_openCover f ğ’°
    intro i
    have hiao : IsAffineOpen (Scheme.Hom.opensRange (ğ’°.map i)) :=
      AlgebraicGeometry.isAffineOpen_opensRange _
    letI : P.RespectsIso := hP.RespectsIso
    rw [â† P.arrow_mk_iso_iff <| morphismRestrictOpensRange f (ğ’°.map i)]
    exact hf (Scheme.Hom.opensRange (ğ’°.map i)) hiao
  Â· intro hf âŸ¨U, hUâŸ©
    exact hP.restrict _ _ hf

/-- The restriction of a morphism property of schemes that is local at the target to morphisms
with affine target, is local. -/
lemma of_isLocal_of_isLocalAtTarget (P : MorphismProperty Scheme)
    (hP : PropertyIsLocalAtTarget P) : (of P).IsLocal where
  RespectsIso := by
    apply AffineTargetMorphismProperty.respectsIso_mk
    Â· intro X Y Z e f _ hf
      apply hP.RespectsIso.precomp e f hf
    Â· intro X Y Z e f _ hf
      apply hP.RespectsIso.postcomp e f hf
  toBasicOpen {X Y} _ f hf := hP.restrict f _
  ofBasicOpenCover {X Y} _ f s hs hf := by
    apply ((hP.openCover_TFAE f).out 0 5).mpr
    let U (r : s) : Opens Y.carrier := Y.basicOpen r.val
    have hiao : IsAffineOpen (âŠ¤ : Opens Y.carrier) := isAffineOpen_top Y
    have hU : iSup U = âŠ¤ := by
      erw [hiao.basicOpen_union_eq_self_iff]
      exact hs
    use s, U, hU, hf

/-- If `P` is local at the target, to show that `P` is stable under base change, it suffices to
check this for base change along a morphism of affine schemes. -/
lemma stableUnderBaseChange_of_stableUnderBaseChangeOnAffine_of_isLocalAtTarget
    (P : MorphismProperty Scheme) (hPâ‚ : PropertyIsLocalAtTarget P)
    (hPâ‚‚ : (of P).StableUnderBaseChange) :
    P.StableUnderBaseChange := by
  rw [â† targetAffineLocally_of_eq_of_isLocalAtTarget P hPâ‚]
  apply (of_isLocal_of_isLocalAtTarget P hPâ‚).stableUnderBaseChange
  exact hPâ‚‚

end AffineTargetMorphismProperty

end Restriction

@[deprecated (since := "2024-06-22")]
alias diagonalTargetAffineLocallyOfOpenCover := diagonal_targetAffineLocally_of_openCover

@[deprecated (since := "2024-06-22")]
alias AffineTargetMorphismProperty.diagonalOfTargetAffineLocally :=
  AffineTargetMorphismProperty.diagonal_of_targetAffineLocally

@[deprecated (since := "2024-06-22")]
alias universallyIsLocalAtTarget := universally_isLocalAtTarget

@[deprecated (since := "2024-06-22")]
alias universallyIsLocalAtTargetOfMorphismRestrict :=
  universally_isLocalAtTarget_of_morphismRestrict

end AlgebraicGeometry
