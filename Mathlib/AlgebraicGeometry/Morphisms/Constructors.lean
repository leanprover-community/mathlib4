/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.Basic
import Mathlib.RingTheory.RingHomProperties

/-!

# Constructors for properties of morphisms between schemes

This file provides some constructors to obtain morphism properties of schemes from other morphism
properties:

- `AffineTargetMorphismProperty.diagonal` : Given an affine target morphism property `P`,
  `P.diagonal f` holds if `P (pullback.mapDesc fâ‚ fâ‚‚ f)` holds for two affine open
  immersions `fâ‚` and `fâ‚‚`.
- `AffineTargetMorphismProperty.of`: Given a morphism property `P` of schemes,
  this is the restriction of `P` to morphisms with affine target. If `P` is local at the
  target, we have `(toAffineTargetMorphismProperty P).targetAffineLocally = P`, see:
  `MorphismProperty.targetAffineLocally_toAffineTargetMorphismProperty_eq_of_isZariskiLocalAtTarget`
- `MorphismProperty.topologically`: Given a property `P` of maps of topological spaces,
  `(topologically P) f` holds if `P` holds for the underlying continuous map of `f`.
- `MorphismProperty.stalkwise`: Given a property `P` of ring homomorphisms,
  `(stalkwise P) f` holds if `P` holds for all stalk maps.

Also provides API for showing the standard locality and stability properties for these
types of properties.

-/

universe u v w

open TopologicalSpace CategoryTheory CategoryTheory.Limits Opposite

noncomputable section

namespace AlgebraicGeometry

section Diagonal

/-- The `AffineTargetMorphismProperty` associated to `(targetAffineLocally P).diagonal`.
See `diagonal_targetAffineLocally_eq_targetAffineLocally`.
-/
def AffineTargetMorphismProperty.diagonal (P : AffineTargetMorphismProperty) :
    AffineTargetMorphismProperty :=
  fun {X _} f _ =>
    âˆ€ â¦ƒUâ‚ Uâ‚‚ : Schemeâ¦„ (fâ‚ : Uâ‚ âŸ¶ X) (fâ‚‚ : Uâ‚‚ âŸ¶ X) [IsAffine Uâ‚] [IsAffine Uâ‚‚] [IsOpenImmersion fâ‚]
      [IsOpenImmersion fâ‚‚], P (pullback.mapDesc fâ‚ fâ‚‚ f)

instance AffineTargetMorphismProperty.diagonal_respectsIso (P : AffineTargetMorphismProperty)
    [P.toProperty.RespectsIso] : P.diagonal.toProperty.RespectsIso := by
  delta AffineTargetMorphismProperty.diagonal
  apply AffineTargetMorphismProperty.respectsIso_mk
  Â· introv H _ _
    rw [pullback.mapDesc_comp, P.cancel_left_of_respectsIso, P.cancel_right_of_respectsIso]
    apply H
  Â· introv H _ _
    rw [pullback.mapDesc_comp, P.cancel_right_of_respectsIso]
    apply H

theorem HasAffineProperty.diagonal_of_openCover (P) {Q} [HasAffineProperty P Q]
    {X Y : Scheme.{u}} (f : X âŸ¶ Y) (ğ’° : Scheme.OpenCover.{v} Y) [âˆ€ i, IsAffine (ğ’°.X i)]
    (ğ’°' : âˆ€ i, Scheme.OpenCover.{w} (pullback f (ğ’°.f i))) [âˆ€ i j, IsAffine ((ğ’°' i).X j)]
    (hğ’°' : âˆ€ i j k,
      Q (pullback.mapDesc ((ğ’°' i).f j) ((ğ’°' i).f k) (ğ’°.pullbackHom f i))) :
    P.diagonal f := by
  letI := isLocal_affineProperty P
  let ğ’± := (Scheme.Pullback.openCoverOfBase ğ’° f f).bind fun i =>
    Scheme.Pullback.openCoverOfLeftRight.{u} (ğ’°' i) (ğ’°' i) (pullback.snd _ _) (pullback.snd _ _)
  have i1 : âˆ€ i, IsAffine (ğ’±.X i) := fun i => by dsimp [ğ’±]; infer_instance
  apply of_openCover ğ’±
  rintro âŸ¨i, j, kâŸ©
  dsimp [ğ’±]
  convert (Q.cancel_left_of_respectsIso
    ((pullbackDiagonalMapIso _ _ ((ğ’°' i).f j) ((ğ’°' i).f k)).inv â‰«
      pullback.map _ _ _ _ (ğŸ™ _) (ğŸ™ _) (ğŸ™ _) _ _) (pullback.snd _ _)).mp _ using 1
  Â· simp
  Â· ext1 <;> simp
  Â· simp only [Category.assoc, limit.lift_Ï€, PullbackCone.mk_pt, PullbackCone.mk_Ï€_app,
      Category.comp_id]
    convert hğ’°' i j k
    ext1 <;> simp [Scheme.Cover.pullbackHom]

theorem HasAffineProperty.diagonal_of_openCover_diagonal
    (P) {Q} [HasAffineProperty P Q]
    {X Y : Scheme.{u}} (f : X âŸ¶ Y) (ğ’° : Scheme.OpenCover Y) [âˆ€ i, IsAffine (ğ’°.X i)]
    (hğ’° : âˆ€ i, Q.diagonal (ğ’°.pullbackHom f i)) :
    P.diagonal f :=
  diagonal_of_openCover P f ğ’° (fun _ â†¦ Scheme.affineCover _)
    (fun _ _ _ â†¦ hğ’° _ _ _)

theorem HasAffineProperty.diagonal_of_diagonal_of_isPullback
    (P) {Q} [HasAffineProperty P Q]
    {X Y U V : Scheme.{u}} {f : X âŸ¶ Y} {g : U âŸ¶ Y}
    [IsAffine U] [IsOpenImmersion g]
    {iV : V âŸ¶ X} {f' : V âŸ¶ U} (h : IsPullback iV f' f g) (H : P.diagonal f) :
    Q.diagonal f' := by
  letI := isLocal_affineProperty P
  rw [â† Q.diagonal.cancel_left_of_respectsIso h.isoPullback.inv,
    h.isoPullback_inv_snd]
  rintro U V fâ‚ fâ‚‚ hU hV hfâ‚ hfâ‚‚
  rw [â† Q.cancel_left_of_respectsIso (pullbackDiagonalMapIso f _ fâ‚ fâ‚‚).hom]
  convert HasAffineProperty.of_isPullback (P := P) (.of_hasPullback _ _) H
  Â· apply pullback.hom_ext <;> simp
  Â· infer_instance
  Â· infer_instance

theorem HasAffineProperty.diagonal_iff
    (P) {Q} [HasAffineProperty P Q] {X Y : Scheme.{u}} {f : X âŸ¶ Y} [IsAffine Y] :
    Q.diagonal f â†” P.diagonal f := by
  letI := isLocal_affineProperty P
  refine âŸ¨fun hf â†¦ ?_, diagonal_of_diagonal_of_isPullback P .of_id_fstâŸ©
  rw [â† Q.diagonal.cancel_left_of_respectsIso
    (pullback.fst (f := f) (g := ğŸ™ Y)), pullback.condition, Category.comp_id] at hf
  let ğ’° := X.affineCover.pushforwardIso (inv (pullback.fst (f := f) (g := ğŸ™ Y)))
  have (i : _) : IsAffine (ğ’°.X i) := by dsimp [ğ’°]; infer_instance
  exact HasAffineProperty.diagonal_of_openCover.{u, u, u} P f (Scheme.coverOfIsIso (ğŸ™ _))
    (fun _ â†¦ ğ’°) (fun _ _ _ â†¦ hf _ _)

theorem AffineTargetMorphismProperty.diagonal_of_openCover_source
    {Q : AffineTargetMorphismProperty} [Q.IsLocal]
    {X Y : Scheme.{u}} (f : X âŸ¶ Y) (ğ’° : Scheme.OpenCover.{v} X) [âˆ€ i, IsAffine (ğ’°.X i)]
    [IsAffine Y] (hğ’° : âˆ€ i j, Q (pullback.mapDesc (ğ’°.f i) (ğ’°.f j) f)) :
    Q.diagonal f := by
  rw [HasAffineProperty.diagonal_iff (targetAffineLocally Q)]
  let ğ’± := Scheme.Pullback.openCoverOfLeftRight.{u} ğ’° ğ’° f f
  have i1 : âˆ€ i, IsAffine (ğ’±.X i) := fun i => by dsimp [ğ’±]; infer_instance
  refine HasAffineProperty.of_openCover (P := targetAffineLocally Q) ğ’± fun i â†¦ ?_
  dsimp [ğ’±, Scheme.Cover.pullbackHom]
  have : IsPullback (pullback.fst _ _ â‰« ğ’°.f _) (pullback.mapDesc (ğ’°.f i.1) (ğ’°.f i.2) f)
      (pullback.diagonal f) (pullback.map _ _ _ _ (ğ’°.f _) (ğ’°.f _) (ğŸ™ Y) (by simp) (by simp)) :=
    .of_iso (pullback_fst_map_snd_isPullback f (ğŸ™ _) (ğ’°.f i.1 â‰« pullback.lift (ğŸ™ _) f)
      (ğ’°.f i.2 â‰« pullback.lift (ğŸ™ _) f)) (asIso (pullback.map _ _ _ _ (ğŸ™ _) (ğŸ™ _)
      (pullback.fst _ _) (by simp) (by simp))) (.refl _) (pullback.congrHom (by simp) (by simp))
      (.refl _) (by simp) (by cat_disch) (by simp) (by cat_disch)
  rw [â† Q.cancel_left_of_respectsIso this.isoPullback.hom, IsPullback.isoPullback_hom_snd]
  exact hğ’° _ _

instance HasAffineProperty.diagonal_affineProperty_isLocal
    {Q : AffineTargetMorphismProperty} [Q.IsLocal] :
    Q.diagonal.IsLocal where
  respectsIso := inferInstance
  to_basicOpen {_ Y} _ f r hf :=
    diagonal_of_diagonal_of_isPullback (targetAffineLocally Q)
      (isPullback_morphismRestrict f (Y.basicOpen r)).flip
      ((diagonal_iff (targetAffineLocally Q)).mp hf)
  of_basicOpenCover {X Y} _ f s hs hs' := by
    refine (diagonal_iff (targetAffineLocally Q)).mpr ?_
    let ğ’° := Y.openCoverOfIsOpenCover _
      ((isAffineOpen_top Y).iSup_basicOpen_eq_self_iff.mpr hs)
    have (i : _) : IsAffine (ğ’°.X i) := (isAffineOpen_top Y).basicOpen i.1
    refine diagonal_of_openCover_diagonal (targetAffineLocally Q) f ğ’° ?_
    intro i
    exact (Q.diagonal.arrow_mk_iso_iff
      (morphismRestrictEq _ (by simp [ğ’°]) â‰ªâ‰« morphismRestrictOpensRange _ _)).mp (hs' i)

instance (P) {Q} [HasAffineProperty P Q] : HasAffineProperty P.diagonal Q.diagonal where
  isLocal_affineProperty := letI := HasAffineProperty.isLocal_affineProperty P; inferInstance
  eq_targetAffineLocally' := by
    ext X Y f
    letI := HasAffineProperty.isLocal_affineProperty P
    constructor
    Â· exact fun H U â†¦ HasAffineProperty.diagonal_of_diagonal_of_isPullback P
        (isPullback_morphismRestrict f U).flip H
    Â· exact fun H â†¦ HasAffineProperty.diagonal_of_openCover_diagonal P f Y.affineCover
        (fun i â†¦ of_targetAffineLocally_of_isPullback (.of_hasPullback _ _) H)

instance (P) [IsZariskiLocalAtTarget P] : IsZariskiLocalAtTarget P.diagonal :=
  letI := HasAffineProperty.of_isZariskiLocalAtTarget P
  inferInstance

open MorphismProperty in
instance (P : MorphismProperty Scheme)
    [P.HasOfPostcompProperty @IsOpenImmersion] [P.RespectsRight @IsOpenImmersion]
    [IsZariskiLocalAtSource P] : IsZariskiLocalAtSource P.diagonal := by
  let g {X Y : Scheme} (f : X âŸ¶ Y) (U : X.Opens) :=
    pullback.map (U.Î¹ â‰« f) (U.Î¹ â‰« f) f f U.Î¹ U.Î¹ (ğŸ™ Y) (by simp) (by simp)
  refine IsZariskiLocalAtSource.mk' (fun {X Y} f U hf â†¦ ?_) (fun {X Y} f {Î¹} U hU hf â†¦ ?_)
  Â· change P _
    apply P.of_postcomp (W' := @IsOpenImmersion) (pullback.diagonal (U.Î¹ â‰« f)) (g f U) inferInstance
    rw [â† pullback.comp_diagonal]
    apply IsZariskiLocalAtSource.comp
    exact hf
  Â· change P _
    refine IsZariskiLocalAtSource.of_iSup_eq_top U hU fun i â†¦ ?_
    rw [pullback.comp_diagonal]
    exact RespectsRight.postcomp (P := P) (Q := @IsOpenImmersion) (g _ _) inferInstance _ (hf i)

end Diagonal

section Universally

theorem universally_isZariskiLocalAtTarget (P : MorphismProperty Scheme)
    (hPâ‚‚ : âˆ€ {X Y : Scheme.{u}} (f : X âŸ¶ Y) {Î¹ : Type u} (U : Î¹ â†’ Y.Opens)
      (_ : IsOpenCover U), (âˆ€ i, P (f âˆ£_ U i)) â†’ P f) : IsZariskiLocalAtTarget P.universally := by
  apply IsZariskiLocalAtTarget.mk'
  Â· exact fun {X Y} f U => P.universally.of_isPullback
      (isPullback_morphismRestrict f U).flip
  Â· intro X Y f Î¹ U hU H X' Y' iâ‚ iâ‚‚ f' h
    apply hPâ‚‚ _ (fun i â†¦ iâ‚‚ â»Â¹áµ U i)
    Â· simp only [IsOpenCover, â† top_le_iff] at hU âŠ¢
      rintro x -
      simpa using @hU (iâ‚‚.base x) trivial
    Â· rintro i
      refine H _ ((X'.isoOfEq ?_).hom â‰« iâ‚ âˆ£_ _) (iâ‚‚ âˆ£_ _) _ ?_
      Â· exact congr($(h.1.1) â»Â¹áµ U i)
      Â· rw [â† (isPullback_morphismRestrict f _).paste_vert_iff]
        Â· simp only [Category.assoc, morphismRestrict_Î¹, Scheme.isoOfEq_hom_Î¹_assoc]
          exact (isPullback_morphismRestrict f' (iâ‚‚ â»Â¹áµ U i)).paste_vert h
        Â· rw [â† cancel_mono (Scheme.Opens.Î¹ _)]
          simp [morphismRestrict_Î¹_assoc, h.1.1]

@[deprecated (since := "2025-10-07")]
alias universally_isLocalAtTarget := universally_isZariskiLocalAtTarget

lemma universally_isZariskiLocalAtSource (P : MorphismProperty Scheme)
    [IsZariskiLocalAtSource P] : IsZariskiLocalAtSource P.universally := by
  refine .mk_of_iff ?_
  intro X Y f ğ’°
  refine âŸ¨fun hf i â†¦ ?_, fun hf â†¦ ?_âŸ©
  Â· apply MorphismProperty.universally_mk'
    intro T g _
    rw [â† P.cancel_left_of_respectsIso (pullbackLeftPullbackSndIso g f _).hom,
      pullbackLeftPullbackSndIso_hom_fst]
    exact IsZariskiLocalAtSource.comp (hf _ _ _ (IsPullback.of_hasPullback ..)) _
  Â· apply MorphismProperty.universally_mk'
    intro T g _
    rw [IsZariskiLocalAtSource.iff_of_openCover (P := P) (ğ’°.pullbackâ‚ <| pullback.snd g f)]
    intro i
    dsimp only [Precoverage.ZeroHypercover.pullbackâ‚_toPreZeroHypercover,
      PreZeroHypercover.pullbackâ‚_X, PreZeroHypercover.pullbackâ‚_f]
    rw [â† pullbackLeftPullbackSndIso_hom_fst, P.cancel_left_of_respectsIso]
    exact hf i _ _ _ (IsPullback.of_hasPullback ..)

@[deprecated (since := "2025-10-07")]
alias universally_isLocalAtSource := universally_isZariskiLocalAtSource

end Universally

section Topologically

/-- `topologically P` holds for a morphism if the underlying topological map satisfies `P`. -/
def topologically
    (P : âˆ€ {Î± Î² : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] (_ : Î± â†’ Î²), Prop) :
    MorphismProperty Scheme.{u} := fun _ _ f => P f.base

variable (P : âˆ€ {Î± Î² : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] (_ : Î± â†’ Î²), Prop)

/-- If a property of maps of topological spaces is stable under composition, the induced
morphism property of schemes is stable under composition. -/
lemma topologically_isStableUnderComposition
    (hP : âˆ€ {Î± Î² Î³ : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³]
      (f : Î± â†’ Î²) (g : Î² â†’ Î³) (_ : P f) (_ : P g), P (g âˆ˜ f)) :
    (topologically P).IsStableUnderComposition where
  comp_mem {X Y Z} f g hf hg := by
    simp only [topologically, Scheme.Hom.comp_base, TopCat.coe_comp]
    exact hP _ _ hf hg

/-- If a property of maps of topological spaces is satisfied by all homeomorphisms,
every isomorphism of schemes satisfies the induced property. -/
lemma topologically_iso_le
    (hP : âˆ€ {Î± Î² : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] (f : Î± â‰ƒâ‚œ Î²), P f) :
    MorphismProperty.isomorphisms Scheme â‰¤ (topologically P) := by
  intro X Y e (he : IsIso e)
  have : IsIso e := he
  exact hP (TopCat.homeoOfIso (asIso e.base))

/-- If a property of maps of topological spaces is satisfied by homeomorphisms and is stable
under composition, the induced property on schemes respects isomorphisms. -/
lemma topologically_respectsIso
    (hPâ‚ : âˆ€ {Î± Î² : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] (f : Î± â‰ƒâ‚œ Î²), P f)
    (hPâ‚‚ : âˆ€ {Î± Î² Î³ : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³]
      (f : Î± â†’ Î²) (g : Î² â†’ Î³) (_ : P f) (_ : P g), P (g âˆ˜ f)) :
      (topologically P).RespectsIso :=
  have : (topologically P).IsStableUnderComposition :=
    topologically_isStableUnderComposition P hPâ‚‚
  MorphismProperty.respectsIso_of_isStableUnderComposition (topologically_iso_le P hPâ‚)

/-- To check that a topologically defined morphism property is local at the target,
we may check the corresponding properties on topological spaces. -/
lemma topologically_isZariskiLocalAtTarget [(topologically P).RespectsIso]
    (hPâ‚‚ : âˆ€ {Î± Î² : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] (f : Î± â†’ Î²) (s : Set Î²)
      (_ : Continuous f) (_ : IsOpen s), P f â†’ P (s.restrictPreimage f))
    (hPâ‚ƒ : âˆ€ {Î± Î² : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] (f : Î± â†’ Î²) {Î¹ : Type u}
      (U : Î¹ â†’ Opens Î²) (_ : IsOpenCover U) (_ : Continuous f),
      (âˆ€ i, P ((U i).carrier.restrictPreimage f)) â†’ P f) :
    IsZariskiLocalAtTarget (topologically P) := by
  apply IsZariskiLocalAtTarget.mk'
  Â· intro X Y f U hf
    simp_rw [topologically, morphismRestrict_base]
    exact hPâ‚‚ f.base U.carrier f.base.hom.2 U.2 hf
  Â· intro X Y f Î¹ U hU hf
    apply hPâ‚ƒ f.base U hU (map_continuous _) fun i â†¦ ?_
    rw [â† morphismRestrict_base]
    exact hf i

@[deprecated (since := "2025-10-07")]
alias topologically_isLocalAtTarget := topologically_isZariskiLocalAtTarget

/-- A variant of `topologically_isZariskiLocalAtTarget`
that takes one iff statement instead of two implications. -/
lemma topologically_isZariskiLocalAtTarget' [(topologically P).RespectsIso]
    (hP : âˆ€ {Î± Î² : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] (f : Î± â†’ Î²) {Î¹ : Type u}
      (U : Î¹ â†’ Opens Î²) (_ : IsOpenCover U) (_ : Continuous f),
      P f â†” (âˆ€ i, P ((U i).carrier.restrictPreimage f))) :
    IsZariskiLocalAtTarget (topologically P) := by
  refine topologically_isZariskiLocalAtTarget P ?_ (fun f _ U hU hU' â†¦ (hP f U hU hU').mpr)
  introv hf hs H
  refine (hP f (![âŠ¤, Opens.mk s hs] âˆ˜ Equiv.ulift) ?_ hf).mp H âŸ¨1âŸ©
  rw [IsOpenCover, â† top_le_iff]
  exact le_iSup (![âŠ¤, Opens.mk s hs] âˆ˜ Equiv.ulift) âŸ¨0âŸ©

@[deprecated (since := "2025-10-07")]
alias topologically_isLocalAtTarget' := topologically_isZariskiLocalAtTarget'

lemma topologically_isZariskiLocalAtSource [(topologically P).RespectsIso]
    (hPâ‚ : âˆ€ {X Y : Type u} [TopologicalSpace X] [TopologicalSpace Y] (f : X â†’ Y)
      (_ : Continuous f) (U : Opens X), P f â†’ P (f âˆ˜ ((â†‘) : U â†’ X)))
    (hPâ‚‚ : âˆ€ {X Y : Type u} [TopologicalSpace X] [TopologicalSpace Y] (f : X â†’ Y)
      (_ : Continuous f) {Î¹ : Type u} (U : Î¹ â†’ Opens X),
      IsOpenCover U â†’ (âˆ€ i, P (f âˆ˜ ((â†‘) : U i â†’ X))) â†’ P f) :
    IsZariskiLocalAtSource (topologically P) := by
  apply IsZariskiLocalAtSource.mk'
  Â· introv hf
    exact hPâ‚ f.base f.continuous _ hf
  Â· introv hU hf
    exact hPâ‚‚ f.base f.continuous _ hU hf

@[deprecated (since := "2025-10-07")]
alias topologically_isLocalAtSource := topologically_isZariskiLocalAtSource

/-- A variant of `topologically_isZariskiLocalAtSource`
that takes one iff statement instead of two implications. -/
lemma topologically_isZariskiLocalAtSource' [(topologically P).RespectsIso]
    (hP : âˆ€ {X Y : Type u} [TopologicalSpace X] [TopologicalSpace Y] (f : X â†’ Y) {Î¹ : Type u}
      (U : Î¹ â†’ Opens X) (_ : IsOpenCover U) (_ : Continuous f),
      P f â†” (âˆ€ i, P (f âˆ˜ ((â†‘) : U i â†’ X)))) :
    IsZariskiLocalAtSource (topologically P) := by
  refine topologically_isZariskiLocalAtSource P ?_ (fun f hf _ U hU hf' â†¦ (hP f U hU hf).mpr hf')
  introv hf hs
  refine (hP f (![âŠ¤, U] âˆ˜ Equiv.ulift) ?_ hf).mp hs âŸ¨1âŸ©
  rw [IsOpenCover, â† top_le_iff]
  exact le_iSup (![âŠ¤, U] âˆ˜ Equiv.ulift) âŸ¨0âŸ©

@[deprecated (since := "2025-10-07")]
alias topologically_isLocalAtSource' := topologically_isZariskiLocalAtSource'

end Topologically

/-- `stalkwise P` holds for a morphism if all stalks satisfy `P`. -/
def stalkwise (P : âˆ€ {R S : Type u} [CommRing R] [CommRing S], (R â†’+* S) â†’ Prop) :
    MorphismProperty Scheme.{u} :=
  fun _ _ f => âˆ€ x, P (f.stalkMap x).hom

section Stalkwise

variable {P : âˆ€ {R S : Type u} [CommRing R] [CommRing S], (R â†’+* S) â†’ Prop}

/-- If `P` respects isos, then `stalkwise P` respects isos. -/
lemma stalkwise_respectsIso (hP : RingHom.RespectsIso P) :
    (stalkwise P).RespectsIso where
  precomp {X Y Z} e (he : IsIso e) f hf := by
    simp only [stalkwise, Scheme.Hom.comp_base, TopCat.coe_comp, Function.comp_apply]
    intro x
    rw [Scheme.Hom.stalkMap_comp]
    exact (RingHom.RespectsIso.cancel_right_isIso hP _ _).mpr <| hf (e.base x)
  postcomp {X Y Z} e (he : IsIso _) f hf := by
    simp only [stalkwise, Scheme.Hom.comp_base, TopCat.coe_comp, Function.comp_apply]
    intro x
    rw [Scheme.Hom.stalkMap_comp]
    exact (RingHom.RespectsIso.cancel_left_isIso hP _ _).mpr <| hf x

/-- If `P` respects isos, then `stalkwise P` is local at the target. -/
lemma stalkwiseIsZariskiLocalAtTarget_of_respectsIso (hP : RingHom.RespectsIso P) :
    IsZariskiLocalAtTarget (stalkwise P) := by
  have hP' : (RingHom.toMorphismProperty P).RespectsIso :=
    RingHom.toMorphismProperty_respectsIso_iff.mp hP
  letI := stalkwise_respectsIso hP
  apply IsZariskiLocalAtTarget.mk'
  Â· intro X Y f U hf x
    apply ((RingHom.toMorphismProperty P).arrow_mk_iso_iff <|
      morphismRestrictStalkMap f U x).mpr <| hf _
  Â· intro X Y f Î¹ U hU hf x
    have hy : f.base x âˆˆ iSup U := by rw [hU]; trivial
    obtain âŸ¨i, hiâŸ© := Opens.mem_iSup.mp hy
    exact ((RingHom.toMorphismProperty P).arrow_mk_iso_iff <|
      morphismRestrictStalkMap f (U i) âŸ¨x, hiâŸ©).mp <| hf i âŸ¨x, hiâŸ©

@[deprecated (since := "2025-10-07")]
alias stalkwiseIsLocalAtTarget_of_respectsIso := stalkwiseIsZariskiLocalAtTarget_of_respectsIso

/-- If `P` respects isos, then `stalkwise P` is local at the source. -/
lemma stalkwise_isZariskiLocalAtSource_of_respectsIso (hP : RingHom.RespectsIso P) :
    IsZariskiLocalAtSource (stalkwise P) := by
  letI := stalkwise_respectsIso hP
  apply IsZariskiLocalAtSource.mk'
  Â· intro X Y f U hf x
    rw [Scheme.Hom.stalkMap_comp, CommRingCat.hom_comp, hP.cancel_right_isIso]
    exact hf _
  Â· intro X Y f Î¹ U hU hf x
    have hy : x âˆˆ iSup U := by rw [hU]; trivial
    obtain âŸ¨i, hiâŸ© := Opens.mem_iSup.mp hy
    rw [â† hP.cancel_right_isIso _ ((U i).Î¹.stalkMap âŸ¨x, hiâŸ©)]
    simpa [Scheme.Hom.stalkMap_comp] using hf i âŸ¨x, hiâŸ©

@[deprecated (since := "2025-10-07")]
alias stalkwise_isLocalAtSource_of_respectsIso := stalkwise_isZariskiLocalAtSource_of_respectsIso

lemma stalkwise_SpecMap_iff (hP : RingHom.RespectsIso P) {R S : CommRingCat} (Ï† : R âŸ¶ S) :
    stalkwise P (Spec.map Ï†) â†” âˆ€ (p : Ideal S) (_ : p.IsPrime),
      P (Localization.localRingHom _ p Ï†.hom rfl) := by
  have hP' : (RingHom.toMorphismProperty P).RespectsIso :=
    RingHom.toMorphismProperty_respectsIso_iff.mp hP
  trans âˆ€ (p : PrimeSpectrum S), P (Localization.localRingHom _ p.asIdeal Ï†.hom rfl)
  Â· exact forall_congr' fun p â†¦
      (RingHom.toMorphismProperty P).arrow_mk_iso_iff (Scheme.arrowStalkMapSpecIso _ _)
  Â· exact âŸ¨fun H p hp â†¦ H âŸ¨p, hpâŸ©, fun H p â†¦ H p.1 p.2âŸ©

@[deprecated (since := "2025-10-07")] alias stalkwise_Spec_map_iff := stalkwise_SpecMap_iff

end Stalkwise

namespace AffineTargetMorphismProperty

/-- If `P` is local at the target, to show that `P` is stable under base change, it suffices to
check this for base change along a morphism of affine schemes. -/
lemma isStableUnderBaseChange_of_isStableUnderBaseChangeOnAffine_of_isZariskiLocalAtTarget
    (P : MorphismProperty Scheme) [IsZariskiLocalAtTarget P]
    (hPâ‚‚ : (of P).IsStableUnderBaseChange) :
    P.IsStableUnderBaseChange :=
  letI := HasAffineProperty.of_isZariskiLocalAtTarget P
  HasAffineProperty.isStableUnderBaseChange hPâ‚‚

end AffineTargetMorphismProperty

end AlgebraicGeometry
