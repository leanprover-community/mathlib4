/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.Basic
import Mathlib.RingTheory.RingHomProperties

/-!

# Constructors for properties of morphisms between schemes

This file provides some constructors to obtain morphism properties of schemes from other morphism
properties:

- `AffineTargetMorphismProperty.diagonal` : Given an affine target morphism property `P`,
  `P.diagonal f` holds if `P (pullback.mapDesc fâ‚ fâ‚‚ f)` holds for two affine open
  immersions `fâ‚` and `fâ‚‚`.
- `AffineTargetMorphismProperty.of`: Given a morphism property `P` of schemes,
  this is the restriction of `P` to morphisms with affine target. If `P` is local at the
  target, we have `(toAffineTargetMorphismProperty P).targetAffineLocally = P`
  (see `MorphismProperty.targetAffineLocally_toAffineTargetMorphismProperty_eq_of_isLocalAtTarget`).
- `MorphismProperty.topologically`: Given a property `P` of maps of topological spaces,
  `(topologically P) f` holds if `P` holds for the underlying continuous map of `f`.
- `MorphismProperty.stalkwise`: Given a property `P` of ring homs,
  `(stalkwise P) f` holds if `P` holds for all stalk maps.

Also provides API for showing the standard locality and stability properties for these
types of properties.

-/

universe u

open TopologicalSpace CategoryTheory CategoryTheory.Limits Opposite

noncomputable section

namespace AlgebraicGeometry

section Diagonal

/-- The `AffineTargetMorphismProperty` associated to `(targetAffineLocally P).diagonal`.
See `diagonal_targetAffineLocally_eq_targetAffineLocally`.
-/
def AffineTargetMorphismProperty.diagonal (P : AffineTargetMorphismProperty) :
    AffineTargetMorphismProperty :=
  fun {X _} f _ =>
    âˆ€ â¦ƒUâ‚ Uâ‚‚ : Schemeâ¦„ (fâ‚ : Uâ‚ âŸ¶ X) (fâ‚‚ : Uâ‚‚ âŸ¶ X) [IsAffine Uâ‚] [IsAffine Uâ‚‚] [IsOpenImmersion fâ‚]
      [IsOpenImmersion fâ‚‚], P (pullback.mapDesc fâ‚ fâ‚‚ f)
#align algebraic_geometry.affine_target_morphism_property.diagonal AlgebraicGeometry.AffineTargetMorphismProperty.diagonal

instance AffineTargetMorphismProperty.diagonal_respectsIso (P : AffineTargetMorphismProperty)
    [P.toProperty.RespectsIso] : P.diagonal.toProperty.RespectsIso := by
  delta AffineTargetMorphismProperty.diagonal
  apply AffineTargetMorphismProperty.respectsIso_mk
  Â· introv H _ _
    rw [pullback.mapDesc_comp, P.cancel_left_of_respectsIso, P.cancel_right_of_respectsIso]
    apply H
  Â· introv H _ _
    rw [pullback.mapDesc_comp, P.cancel_right_of_respectsIso]
    apply H
#align algebraic_geometry.affine_target_morphism_property.diagonal_respects_iso AlgebraicGeometry.AffineTargetMorphismProperty.diagonal_respectsIso

theorem HasAffineProperty.diagonal_of_openCover
    (P) [HasAffineProperty P]
    {X Y : Scheme.{u}} (f : X âŸ¶ Y) (ğ’° : Scheme.OpenCover.{u} Y) [âˆ€ i, IsAffine (ğ’°.obj i)]
    (ğ’°' : âˆ€ i, Scheme.OpenCover.{u} (pullback f (ğ’°.map i))) [âˆ€ i j, IsAffine ((ğ’°' i).obj j)]
    (hğ’°' : âˆ€ i j k,
      P.affineProperty (pullback.mapDesc ((ğ’°' i).map j) ((ğ’°' i).map k) (ğ’°.pullbackHom f i))) :
    P.diagonal f := by
  let ğ’± := (Scheme.Pullback.openCoverOfBase ğ’° f f).bind fun i =>
    Scheme.Pullback.openCoverOfLeftRight.{u} (ğ’°' i) (ğ’°' i) (pullback.snd _ _) (pullback.snd _ _)
  have i1 : âˆ€ i, IsAffine (ğ’±.obj i) := fun i => by dsimp [ğ’±]; infer_instance
  apply of_openCover ğ’±
  rintro âŸ¨i, j, kâŸ©
  dsimp [ğ’±]
  convert (P.affineProperty.cancel_left_of_respectsIso
    ((pullbackDiagonalMapIso _ _ ((ğ’°' i).map j) ((ğ’°' i).map k)).inv â‰«
      pullback.map _ _ _ _ (ğŸ™ _) (ğŸ™ _) (ğŸ™ _) _ _) pullback.snd).mp _ using 1
  Â· simp
  Â· ext <;> simp
  Â· simp only [Category.assoc, limit.lift_Ï€, PullbackCone.mk_pt, PullbackCone.mk_Ï€_app,
      Functor.const_obj_obj, cospan_one, cospan_left, cospan_right, Category.comp_id]
    convert hğ’°' i j k
    ext <;> simp [Scheme.OpenCover.pullbackHom]
#align algebraic_geometry.diagonal_target_affine_locally_of_open_cover AlgebraicGeometry.HasAffineProperty.diagonal_of_openCover

theorem HasAffineProperty.diagonal_of_openCover_diagonal_affineProperty
    (P) [HasAffineProperty P]
    {X Y : Scheme.{u}} (f : X âŸ¶ Y) (ğ’° : Scheme.OpenCover.{u} Y) [âˆ€ i, IsAffine (ğ’°.obj i)]
    (hğ’° : âˆ€ i, P.affineProperty.diagonal (ğ’°.pullbackHom f i)) :
    P.diagonal f :=
  HasAffineProperty.diagonal_of_openCover P f ğ’° (fun _ â†¦ Scheme.affineCover _)
    (fun _ _ _ â†¦ hğ’° _ _ _)

instance {X} [IsAffine X] (i) : IsAffine ((Scheme.openCoverOfIsIso (ğŸ™ X)).obj i) := by
  dsimp; infer_instance

theorem HasAffineProperty.affineProperty_diagonal_of_diagonal_of_isPullback
    (P) [HasAffineProperty P]
    {X Y U V : Scheme.{u}} {f : X âŸ¶ Y} {g : U âŸ¶ Y}
    [IsAffine U] [IsOpenImmersion g]
    {iV : V âŸ¶ X} {f' : V âŸ¶ U} (h : IsPullback iV f' f g) (H : P.diagonal f) :
    P.affineProperty.diagonal f' := by
  rw [â† P.affineProperty.diagonal.cancel_left_of_respectsIso h.isoPullback.inv,
    h.isoPullback_inv_snd]
  rintro U V fâ‚ fâ‚‚ hU hV hfâ‚ hfâ‚‚
  rw [â† P.affineProperty.cancel_left_of_respectsIso (pullbackDiagonalMapIso f _ fâ‚ fâ‚‚).hom]
  convert HasAffineProperty.of_isPullback (P := P) (.of_hasPullback _ _) H
  Â· apply pullback.hom_ext <;> simp
  Â· infer_instance
  Â· infer_instance
#align algebraic_geometry.affine_target_morphism_property.diagonal_of_target_affine_locally AlgebraicGeometry.HasAffineProperty.affineProperty_diagonal_of_diagonal_of_isPullback

lemma _root_.CategoryTheory.IsPullback.of_id_fst {C} [Category C] {X Y : C} (f : X âŸ¶ Y) :
    IsPullback (ğŸ™ _) f f (ğŸ™ _) := IsPullback.of_horiz_isIso âŸ¨by simpâŸ©

lemma _root_.CategoryTheory.IsPullback.of_id_snd {C} [Category C] {X Y : C} (f : X âŸ¶ Y) :
  IsPullback f (ğŸ™ _) (ğŸ™ _) f := IsPullback.of_vert_isIso âŸ¨by simpâŸ©

theorem HasAffineProperty.of_affineProperty_diagonal
    {P} [HasAffineProperty P] {X Y} {f : X âŸ¶ Y} [IsAffine Y] :
    P.affineProperty.diagonal f â†” P.diagonal f := by
  refine âŸ¨fun hf â†¦ ?_, affineProperty_diagonal_of_diagonal_of_isPullback P (.of_id_fst _)âŸ©
  rw [â† P.affineProperty.diagonal.cancel_left_of_respectsIso
    (pullback.fst (f := f) (g := ğŸ™ Y)), pullback.condition, Category.comp_id] at hf
  let ğ’° := X.affineCover.pushforwardIso (inv (pullback.fst (f := f) (g := ğŸ™ Y)))
  have (i) : IsAffine (ğ’°.obj i) := by dsimp [ğ’°]; infer_instance
  exact HasAffineProperty.diagonal_of_openCover P f (Scheme.openCoverOfIsIso (ğŸ™ _))
    (fun _ â†¦ ğ’°) (fun _ _ _ â†¦ hf _ _)

theorem HasAffineProperty.affineProperty_diagonal (P) [HasAffineProperty P] :
    P.affineProperty.diagonal = AffineTargetMorphismProperty.of P.diagonal := by
  ext X Y f _
  exact of_affineProperty_diagonal

instance HasAffineProperty.diagonal_affineProperty_isLocal (P) [HasAffineProperty P] :
    P.affineProperty.diagonal.IsLocal where
  respectsIso := inferInstance
  to_basicOpen {X Y} _ f r hf :=
    have : IsAffine (Y âˆ£_áµ¤ Y.basicOpen r) := (isAffineOpen_top Y).basicOpen r
    affineProperty_diagonal_of_diagonal_of_isPullback P
      (isPullback_morphismRestrict f (Y.basicOpen r)).flip
      (of_affineProperty_diagonal.mp hf)
  of_basicOpenCover {X Y} _ f s hs hs' := by
    refine of_affineProperty_diagonal.mpr ?_
    let ğ’° := Y.openCoverOfSuprEqTop _ (((isAffineOpen_top Y).basicOpen_union_eq_self_iff _).mpr hs)
    have (i) : IsAffine (ğ’°.obj i) := (isAffineOpen_top Y).basicOpen i.1
    refine diagonal_of_openCover_diagonal_affineProperty P f
      (Y.openCoverOfSuprEqTop _ (((isAffineOpen_top Y).basicOpen_union_eq_self_iff _).mpr hs)) ?_
    intro i
    exact (P.affineProperty.diagonal.arrow_mk_iso_iff
      (morphismRestrictEq _ (by simp) â‰ªâ‰« morphismRestrictOpensRange _ _)).mp (hs' i)
#align algebraic_geometry.affine_target_morphism_property.is_local.diagonal AlgebraicGeometry.HasAffineProperty.diagonal_affineProperty_isLocal

theorem HasAffineProperty.targetAffineLocally_diagonal_affineProperty (P) [HasAffineProperty P] :
    targetAffineLocally P.affineProperty.diagonal = P.diagonal := by
  ext X Y f
  constructor
  Â· exact fun H â†¦ diagonal_of_openCover_diagonal_affineProperty P f Y.affineCover
      (fun i â†¦ of_targetAffineLocally_of_isPullback (.of_hasPullback _ _) H)
  Â· exact fun H U â†¦ affineProperty_diagonal_of_diagonal_of_isPullback P
      (isPullback_morphismRestrict f U).flip H
#align algebraic_geometry.diagonal_target_affine_locally_eq_target_affine_locally AlgebraicGeometry.HasAffineProperty.targetAffineLocally_diagonal_affineProperty

instance (P) [HasAffineProperty P] : HasAffineProperty P.diagonal where
  affineProperty := P.affineProperty.diagonal
  isLocal_affineProperty := inferInstance
  eq_targetAffineLocally' := (HasAffineProperty.targetAffineLocally_diagonal_affineProperty P).symm

instance (P) [IsLocalAtTarget P] : IsLocalAtTarget P.diagonal :=
  letI := hasAffinePropertyOfIsLocalAtTarget P
  inferInstance

end Diagonal

section Universally

theorem universally_isLocalAtTarget (P : MorphismProperty Scheme)
    [P.RespectsIso]
    (hPâ‚‚ : âˆ€ {X Y : Scheme.{u}} (f : X âŸ¶ Y) {Î¹ : Type u} (U : Î¹ â†’ Opens Y.carrier)
      (_ : iSup U = âŠ¤), (âˆ€ i, P (f âˆ£_ U i)) â†’ P f) : IsLocalAtTarget P.universally := by
  refine âŸ¨P.universally_respectsIso, fun {X Y} f U =>
    P.universally_stableUnderBaseChange (isPullback_morphismRestrict f U).flip, ?_âŸ©
  intros X Y f Us hUs H X' Y' iâ‚ iâ‚‚ f' h
  apply hPâ‚‚ _ (fun i : Us â†¦ iâ‚‚ â»Â¹áµ i.1)
  Â· rw [â† top_le_iff] at hUs âŠ¢
    rintro x -
    simpa using @hUs (iâ‚‚.1.base x) trivial
  Â· rintro U
    refine H _ U.2 ((X'.restrictIsoOfEq ?_).hom â‰« iâ‚ âˆ£_ _) (iâ‚‚ âˆ£_ _) _ ?_
    Â· exact congr($(h.1.1) â»Â¹áµ U.1)
    Â· rw [â† (isPullback_morphismRestrict f U.1).paste_vert_iff]
      Â· simp only [Scheme.restrictIsoOfEq, Category.assoc, morphismRestrict_Î¹,
          IsOpenImmersion.isoOfRangeEq_hom_fac_assoc]
        exact (isPullback_morphismRestrict f' (iâ‚‚ â»Â¹áµ U)).paste_vert h
      Â· rw [â† cancel_mono (Scheme.Î¹Opens _)]
        simp [IsOpenImmersion.isoOfRangeEq_hom_fac_assoc, Scheme.restrictIsoOfEq,
          morphismRestrict_Î¹_assoc, h.1.1]

#align algebraic_geometry.universally_is_local_at_target_of_morphism_restrict AlgebraicGeometry.universally_isLocalAtTarget

end Universally

namespace MorphismProperty

section Topologically

/-- `topologically P` holds for a morphism if the underlying topological map satisfies `P`. -/
def topologically
    (P : âˆ€ {Î± Î² : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] (_ : Î± â†’ Î²), Prop) :
    MorphismProperty Scheme.{u} := fun _ _ f => P f.1.base
#align algebraic_geometry.morphism_property.topologically AlgebraicGeometry.MorphismProperty.topologically

variable (P : âˆ€ {Î± Î² : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] (_ : Î± â†’ Î²), Prop)

/-- If a property of maps of topological spaces is stable under composition, the induced
morphism property of schemes is stable under composition. -/
lemma topologically_isStableUnderComposition
    (hP : âˆ€ {Î± Î² Î³ : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³]
      (f : Î± â†’ Î²) (g : Î² â†’ Î³) (_ : P f) (_ : P g), P (g âˆ˜ f)) :
    (MorphismProperty.topologically P).IsStableUnderComposition where
  comp_mem {X Y Z} f g hf hg := by
    simp only [MorphismProperty.topologically, Scheme.comp_coeBase, TopCat.coe_comp]
    exact hP _ _ hf hg

/-- If a property of maps of topological spaces is satisfied by all homeomorphisms,
every isomorphism of schemes satisfies the induced property. -/
lemma topologically_iso_le
    (hP : âˆ€ {Î± Î² : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] (f : Î± â‰ƒâ‚œ Î²), P f) :
    MorphismProperty.isomorphisms Scheme â‰¤ (MorphismProperty.topologically P) := by
  intro X Y e (he : IsIso e)
  have : IsIso e := he
  exact hP (TopCat.homeoOfIso (asIso e.val.base))

/-- If a property of maps of topological spaces is satisfied by homeomorphisms and is stable
under composition, the induced property on schemes respects isomorphisms. -/
lemma topologically_respectsIso
    (hPâ‚ : âˆ€ {Î± Î² : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] (f : Î± â‰ƒâ‚œ Î²), P f)
    (hPâ‚‚ : âˆ€ {Î± Î² Î³ : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³]
      (f : Î± â†’ Î²) (g : Î² â†’ Î³) (_ : P f) (_ : P g), P (g âˆ˜ f)) :
      (MorphismProperty.topologically P).RespectsIso :=
  have : (MorphismProperty.topologically P).IsStableUnderComposition :=
    topologically_isStableUnderComposition P hPâ‚‚
  MorphismProperty.respectsIso_of_isStableUnderComposition (topologically_iso_le P hPâ‚)

/-- To check that a topologically defined morphism property is local at the target,
we may check the corresponding properties on topological spaces. -/
lemma topologically_propertyIsLocalAtTarget
    [(MorphismProperty.topologically P).RespectsIso]
    (hPâ‚‚ : âˆ€ {Î± Î² : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] (f : Î± â†’ Î²) (s : Set Î²),
      P f â†’ P (s.restrictPreimage f))
    (hPâ‚ƒ : âˆ€ {Î± Î² : Type u} [TopologicalSpace Î±] [TopologicalSpace Î²] (f : Î± â†’ Î²) {Î¹ : Type u}
      (U : Î¹ â†’ TopologicalSpace.Opens Î²) (_ : iSup U = âŠ¤) (_ : Continuous f),
      (âˆ€ i, P ((U i).carrier.restrictPreimage f)) â†’ P f) :
    IsLocalAtTarget (MorphismProperty.topologically P) := by
  constructor
  Â· infer_instance
  Â· intro X Y f U hf
    simp_rw [MorphismProperty.topologically, morphismRestrict_val_base]
    exact hPâ‚‚ f.val.base U.carrier hf
  Â· intro X Y f Us hU hf
    apply hPâ‚ƒ f.val.base (fun i : Us â†¦ i.1) (by simpa using hU) f.val.base.continuous fun i â†¦ ?_
    rw [â† morphismRestrict_val_base]
    exact hf i i.2

end Topologically

/-- `stalkwise P` holds for a morphism if all stalks satisfy `P`. -/
def stalkwise (P : âˆ€ {R S : Type u} [CommRing R] [CommRing S], (R â†’+* S) â†’ Prop) :
    MorphismProperty Scheme.{u} :=
  fun _ _ f => âˆ€ x, P (PresheafedSpace.stalkMap f.val x)

section Stalkwise

variable {P : âˆ€ {R S : Type u} [CommRing R] [CommRing S], (R â†’+* S) â†’ Prop}

/-- If `P` respects isos, then `stalkwise P` respects isos. -/
lemma stalkwise_respectsIso (hP : RingHom.RespectsIso P) :
    (MorphismProperty.stalkwise P).RespectsIso where
  precomp {X Y Z} e f hf := by
    simp only [stalkwise, Scheme.comp_coeBase, TopCat.coe_comp, Function.comp_apply]
    intro x
    erw [PresheafedSpace.stalkMap.comp]
    exact (RingHom.RespectsIso.cancel_right_isIso hP _ _).mpr <| hf (e.hom.val.base x)
  postcomp {X Y Z} e f hf := by
    simp only [stalkwise, Scheme.comp_coeBase, TopCat.coe_comp, Function.comp_apply]
    intro x
    erw [PresheafedSpace.stalkMap.comp]
    exact (RingHom.RespectsIso.cancel_left_isIso hP _ _).mpr <| hf x

/-- If `P` respects isos, then `stalkwise P` is local at the target. -/
lemma stalkwiseIsLocalAtTarget_of_respectsIso (hP : RingHom.RespectsIso P) :
    PropertyIsLocalAtTarget (MorphismProperty.stalkwise P) := by
  have hP' : (RingHom.toMorphismProperty P).RespectsIso :=
    RingHom.toMorphismProperty_respectsIso_iff.mp hP
  letI := stalkwise_respectsIso hP
  apply propertyIsLocalAtTarget_of_morphismRestrict
  Â· intro X Y f U hf x
    apply ((RingHom.toMorphismProperty P).arrow_mk_iso_iff <|
      morphismRestrictStalkMap f U x).mpr <| hf _
  Â· intro X Y f Î¹ U hU hf x
    have hy : f.val.base x âˆˆ iSup U := by rw [hU]; trivial
    obtain âŸ¨i, hiâŸ© := Opens.mem_iSup.mp hy
    exact ((RingHom.toMorphismProperty P).arrow_mk_iso_iff <|
      morphismRestrictStalkMap f (U i) âŸ¨x, hiâŸ©).mp <| hf i âŸ¨x, hiâŸ©

end Stalkwise

end MorphismProperty

section Restriction

/-- If `P` is a property of scheme morphisms, we may restrict `P` to morphisms with affine target
to obtain an `AffineTargetMorphismProperty`. -/
def AffineTargetMorphismProperty.of (P : MorphismProperty Scheme) :
    AffineTargetMorphismProperty := fun _ _ f â†¦ P f

namespace AffineTargetMorphismProperty

/-- Restricting a local at the target morphism property of schemes `P` to morphisms with affine
target and extending to a global property with `targetAffineLocally` yields `P` again,
if `P` is local at the target. -/
lemma targetAffineLocally_of_eq_of_isLocalAtTarget
    (P : MorphismProperty Scheme) (hP : PropertyIsLocalAtTarget P) :
    targetAffineLocally (of P) = P := by
  ext X Y f
  constructor
  Â· intro hf
    simp only [targetAffineLocally, Subtype.forall] at hf
    let ğ’° : Y.OpenCover := Y.affineCover
    apply hP.of_openCover f ğ’°
    intro i
    have hiao : IsAffineOpen (Scheme.Hom.opensRange (ğ’°.map i)) :=
      AlgebraicGeometry.isAffineOpen_opensRange _
    letI : P.RespectsIso := hP.RespectsIso
    rw [â† P.arrow_mk_iso_iff <| morphismRestrictOpensRange f (ğ’°.map i)]
    exact hf (Scheme.Hom.opensRange (ğ’°.map i)) hiao
  Â· intro hf âŸ¨U, hUâŸ©
    exact hP.restrict _ _ hf

/-- The restriction of a morphism property of schemes that is local at the target to morphisms
with affine target, is local. -/
lemma of_isLocal_of_isLocalAtTarget (P : MorphismProperty Scheme)
    (hP : PropertyIsLocalAtTarget P) : (of P).IsLocal where
  RespectsIso := by
    apply AffineTargetMorphismProperty.respectsIso_mk
    Â· intro X Y Z e f _ hf
      apply hP.RespectsIso.precomp e f hf
    Â· intro X Y Z e f _ hf
      apply hP.RespectsIso.postcomp e f hf
  toBasicOpen {X Y} _ f hf := hP.restrict f _
  ofBasicOpenCover {X Y} _ f s hs hf := by
    apply ((hP.openCover_TFAE f).out 0 5).mpr
    let U (r : s) : Opens Y.carrier := Y.basicOpen r.val
    have hiao : IsAffineOpen (âŠ¤ : Opens Y.carrier) := isAffineOpen_top Y
    have hU : iSup U = âŠ¤ := by
      erw [hiao.basicOpen_union_eq_self_iff]
      exact hs
    use s, U, hU, hf

/-- If `P` is local at the target, to show that `P` is stable under base change, it suffices to
check this for base change along a morphism of affine schemes. -/
lemma stableUnderBaseChange_of_stableUnderBaseChangeOnAffine_of_isLocalAtTarget
    (P : MorphismProperty Scheme) (hPâ‚ : PropertyIsLocalAtTarget P)
    (hPâ‚‚ : (of P).StableUnderBaseChange) :
    P.StableUnderBaseChange := by
  rw [â† targetAffineLocally_of_eq_of_isLocalAtTarget P hPâ‚]
  apply (of_isLocal_of_isLocalAtTarget P hPâ‚).stableUnderBaseChange
  exact hPâ‚‚

end AffineTargetMorphismProperty

end Restriction

@[deprecated (since := "2024-06-22")]
alias diagonalTargetAffineLocallyOfOpenCover := HasAffineProperty.diagonal_of_openCover

@[deprecated (since := "2024-06-22")]
alias AffineTargetMorphismProperty.diagonalOfTargetAffineLocally :=
  HasAffineProperty.affineProperty_diagonal_of_diagonal_of_isPullback

@[deprecated (since := "2024-06-22")]
alias universallyIsLocalAtTarget := universally_isLocalAtTarget

@[deprecated (since := "2024-06-22")]
alias universallyIsLocalAtTargetOfMorphismRestrict :=
  universally_isLocalAtTarget

end AlgebraicGeometry
