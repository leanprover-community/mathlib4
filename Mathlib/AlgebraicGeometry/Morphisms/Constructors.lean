/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.Basic

/-!

# Constructors for properties of morphisms between schemes

This file provides some constructors to obtain morphism properties of schemes from other morphism
properties:

- `AffineTargetMorphismProperty.diagonal` : Given an affine target morphism property `P`,
  `P.diagonal f` holds if `P (pullback.mapDesc f‚ÇÅ f‚ÇÇ f)` holds for two affine open
  immersions `f‚ÇÅ` and `f‚ÇÇ`.
- `MorphismProperty.topologically`: Given a property `P` of maps of topological spaces,
  `(topologically P) f` holds if `P` holds for the underlying continuous map of `f`.

Also provides API for showing the standard locality and stability properties for these
types of properties.

-/

universe u

open TopologicalSpace CategoryTheory CategoryTheory.Limits Opposite

noncomputable section

namespace AlgebraicGeometry

section Diagonal

/-- The `AffineTargetMorphismProperty` associated to `(targetAffineLocally P).diagonal`.
See `diagonal_targetAffineLocally_eq_targetAffineLocally`.
-/
def AffineTargetMorphismProperty.diagonal (P : AffineTargetMorphismProperty) :
    AffineTargetMorphismProperty :=
  fun {X _} f _ =>
    ‚àÄ ‚¶ÉU‚ÇÅ U‚ÇÇ : Scheme‚¶Ñ (f‚ÇÅ : U‚ÇÅ ‚ü∂ X) (f‚ÇÇ : U‚ÇÇ ‚ü∂ X) [IsAffine U‚ÇÅ] [IsAffine U‚ÇÇ] [IsOpenImmersion f‚ÇÅ]
      [IsOpenImmersion f‚ÇÇ], P (pullback.mapDesc f‚ÇÅ f‚ÇÇ f)
#align algebraic_geometry.affine_target_morphism_property.diagonal AlgebraicGeometry.AffineTargetMorphismProperty.diagonal

instance AffineTargetMorphismProperty.diagonal_respectsIso (P : AffineTargetMorphismProperty)
    [P.toProperty.RespectsIso] : P.diagonal.toProperty.RespectsIso := by
  delta AffineTargetMorphismProperty.diagonal
  apply AffineTargetMorphismProperty.respectsIso_mk
  ¬∑ introv H _ _
    rw [pullback.mapDesc_comp, P.cancel_left_of_respectsIso, P.cancel_right_of_respectsIso]
    apply H
  ¬∑ introv H _ _
    rw [pullback.mapDesc_comp, P.cancel_right_of_respectsIso]
    apply H
#align algebraic_geometry.affine_target_morphism_property.diagonal_respects_iso AlgebraicGeometry.AffineTargetMorphismProperty.diagonal_respectsIso

theorem HasAffineProperty.diagonal_of_openCover
    (P) [HasAffineProperty P]
    {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (ùí∞ : Scheme.OpenCover.{u} Y) [‚àÄ i, IsAffine (ùí∞.obj i)]
    (ùí∞' : ‚àÄ i, Scheme.OpenCover.{u} (pullback f (ùí∞.map i))) [‚àÄ i j, IsAffine ((ùí∞' i).obj j)]
    (hùí∞' : ‚àÄ i j k,
      P.affineProperty (pullback.mapDesc ((ùí∞' i).map j) ((ùí∞' i).map k) (ùí∞.pullbackHom f i))) :
    P.diagonal f := by
  let ùí± := (Scheme.Pullback.openCoverOfBase ùí∞ f f).bind fun i =>
    Scheme.Pullback.openCoverOfLeftRight.{u} (ùí∞' i) (ùí∞' i) pullback.snd pullback.snd
  have i1 : ‚àÄ i, IsAffine (ùí±.obj i) := fun i => by dsimp [ùí±]; infer_instance
  apply of_openCover ùí±
  rintro ‚ü®i, j, k‚ü©
  dsimp [ùí±]
  convert (P.affineProperty.cancel_left_of_respectsIso
    ((pullbackDiagonalMapIso _ _ ((ùí∞' i).map j) ((ùí∞' i).map k)).inv ‚â´
      pullback.map _ _ _ _ (ùüô _) (ùüô _) (ùüô _) _ _) pullback.snd).mp _ using 1
  ¬∑ simp
  ¬∑ ext <;> simp
  ¬∑ simp only [Category.assoc, limit.lift_œÄ, PullbackCone.mk_pt, PullbackCone.mk_œÄ_app,
      Functor.const_obj_obj, cospan_one, cospan_left, cospan_right, Category.comp_id]
    convert hùí∞' i j k
    ext <;> simp [Scheme.OpenCover.pullbackHom]
#align algebraic_geometry.diagonal_target_affine_locally_of_open_cover AlgebraicGeometry.HasAffineProperty.diagonal_of_openCover

theorem HasAffineProperty.diagonal_of_openCover_diagonal_affineProperty
    (P) [HasAffineProperty P]
    {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (ùí∞ : Scheme.OpenCover.{u} Y) [‚àÄ i, IsAffine (ùí∞.obj i)]
    (hùí∞ : ‚àÄ i, P.affineProperty.diagonal (ùí∞.pullbackHom f i)) :
    P.diagonal f :=
  HasAffineProperty.diagonal_of_openCover P f ùí∞ (fun _ ‚Ü¶ Scheme.affineCover _)
    (fun _ _ _ ‚Ü¶ hùí∞ _ _ _)

instance {X} [IsAffine X] (i) : IsAffine ((Scheme.openCoverOfIsIso (ùüô X)).obj i) := by
  dsimp; infer_instance

theorem HasAffineProperty.affineProperty_diagonal_of_diagonal_of_isPullback
    (P) [HasAffineProperty P]
    {X Y U V : Scheme.{u}} {f : X ‚ü∂ Y} {g : U ‚ü∂ Y}
    [IsAffine U] [IsOpenImmersion g]
    {iV : V ‚ü∂ X} {f' : V ‚ü∂ U} (h : IsPullback iV f' f g) (H : P.diagonal f) :
    P.affineProperty.diagonal f' := by
  rw [‚Üê P.affineProperty.diagonal.cancel_left_of_respectsIso h.isoPullback.inv,
    h.isoPullback_inv_snd]
  rintro U V f‚ÇÅ f‚ÇÇ hU hV hf‚ÇÅ hf‚ÇÇ
  rw [‚Üê P.affineProperty.cancel_left_of_respectsIso (pullbackDiagonalMapIso f _ f‚ÇÅ f‚ÇÇ).hom]
  convert HasAffineProperty.of_isPullback (P := P) (.of_hasPullback _ _) H
  ¬∑ apply pullback.hom_ext <;> simp
  ¬∑ infer_instance
  ¬∑ infer_instance
#align algebraic_geometry.affine_target_morphism_property.diagonal_of_target_affine_locally AlgebraicGeometry.HasAffineProperty.affineProperty_diagonal_of_diagonal_of_isPullback

lemma _root_.CategoryTheory.IsPullback.of_id_fst {C} [Category C] {X Y : C} (f : X ‚ü∂ Y) :
  IsPullback (ùüô _) f f (ùüô _) := IsPullback.of_horiz_isIso ‚ü®by simp‚ü©

lemma _root_.CategoryTheory.IsPullback.of_id_snd {C} [Category C] {X Y : C} (f : X ‚ü∂ Y) :
  IsPullback f (ùüô _) (ùüô _) f := IsPullback.of_vert_isIso ‚ü®by simp‚ü©

theorem HasAffineProperty.of_affineProperty_diagonal
    {P} [HasAffineProperty P] {X Y} {f : X ‚ü∂ Y} [IsAffine Y] :
    P.affineProperty.diagonal f ‚Üî P.diagonal f := by
  refine ‚ü®fun hf ‚Ü¶ ?_, affineProperty_diagonal_of_diagonal_of_isPullback P (.of_id_fst _)‚ü©
  rw [‚Üê P.affineProperty.diagonal.cancel_left_of_respectsIso
    (pullback.fst (f := f) (g := ùüô Y)), pullback.condition, Category.comp_id] at hf
  let ùí∞ := X.affineCover.pushforwardIso (inv (pullback.fst (f := f) (g := ùüô Y)))
  have (i) : IsAffine (ùí∞.obj i) := by dsimp [ùí∞]; infer_instance
  exact HasAffineProperty.diagonal_of_openCover P f (Scheme.openCoverOfIsIso (ùüô _))
    (fun _ ‚Ü¶ ùí∞) (fun _ _ _ ‚Ü¶ hf _ _)

theorem HasAffineProperty.affineProperty_diagonal (P) [HasAffineProperty P] :
    P.affineProperty.diagonal = AffineTargetMorphismProperty.of P.diagonal := by
  ext X Y f _
  exact of_affineProperty_diagonal

instance HasAffineProperty.diagonal_affineProperty_isLocal (P) [HasAffineProperty P] :
    P.affineProperty.diagonal.IsLocal where
  respectsIso := inferInstance
  to_basicOpen {X Y} _ f r hf :=
    have : IsAffine (Y ‚à£_·µ§ Y.basicOpen r) := (isAffineOpen_top Y).basicOpen r
    affineProperty_diagonal_of_diagonal_of_isPullback P
      (isPullback_morphismRestrict f (Y.basicOpen r)).flip
      (of_affineProperty_diagonal.mp hf)
  of_basicOpenCover {X Y} _ f s hs hs' := by
    refine of_affineProperty_diagonal.mpr ?_
    let ùí∞ := Y.openCoverOfSuprEqTop _ (((isAffineOpen_top Y).basicOpen_union_eq_self_iff _).mpr hs)
    have (i) : IsAffine (ùí∞.obj i) := (isAffineOpen_top Y).basicOpen i.1
    refine diagonal_of_openCover_diagonal_affineProperty P f
      (Y.openCoverOfSuprEqTop _ (((isAffineOpen_top Y).basicOpen_union_eq_self_iff _).mpr hs)) ?_
    intro i
    exact (P.affineProperty.diagonal.arrow_mk_iso_iff
      (morphismRestrictEq _ (by simp) ‚â™‚â´ morphismRestrictOpensRange _ _)).mp (hs' i)
#align algebraic_geometry.affine_target_morphism_property.is_local.diagonal AlgebraicGeometry.HasAffineProperty.diagonal_affineProperty_isLocal

theorem HasAffineProperty.targetAffineLocally_diagonal_affineProperty (P) [HasAffineProperty P] :
    targetAffineLocally P.affineProperty.diagonal = P.diagonal := by
  ext X Y f
  constructor
  ¬∑ exact fun H ‚Ü¶ diagonal_of_openCover_diagonal_affineProperty P f Y.affineCover
      (fun i ‚Ü¶ of_targetAffineLocally_of_isPullback (.of_hasPullback _ _) H)
  ¬∑ exact fun H U ‚Ü¶ affineProperty_diagonal_of_diagonal_of_isPullback P
      (isPullback_morphismRestrict f U).flip H
#align algebraic_geometry.diagonal_target_affine_locally_eq_target_affine_locally AlgebraicGeometry.HasAffineProperty.targetAffineLocally_diagonal_affineProperty

instance (P) [HasAffineProperty P] : HasAffineProperty P.diagonal where
  affineProperty := P.affineProperty.diagonal
  isLocal_affineProperty := inferInstance
  eq_targetAffineLocally' := (HasAffineProperty.targetAffineLocally_diagonal_affineProperty P).symm

instance (P) [IsLocalAtTarget P] : IsLocalAtTarget P.diagonal :=
  letI := hasAffinePropertyOfIsLocalAtTarget P
  inferInstance

end Diagonal

section Universally

theorem universally_isLocalAtTarget (P : MorphismProperty Scheme)
    [P.RespectsIso]
    (hP‚ÇÇ : ‚àÄ {X Y : Scheme.{u}} (f : X ‚ü∂ Y) {Œπ : Type u} (U : Œπ ‚Üí Opens Y.carrier)
      (_ : iSup U = ‚ä§), (‚àÄ i, P (f ‚à£_ U i)) ‚Üí P f) : IsLocalAtTarget P.universally := by
  refine ‚ü®P.universally_respectsIso, fun {X Y} f U =>
    P.universally_stableUnderBaseChange (isPullback_morphismRestrict f U).flip, ?_‚ü©
  intros X Y f Us hUs H X' Y' i‚ÇÅ i‚ÇÇ f' h
  apply hP‚ÇÇ _ (fun i : Us ‚Ü¶ i‚ÇÇ ‚Åª¬π·µÅ i.1)
  ¬∑ rw [‚Üê top_le_iff] at hUs ‚ä¢
    rintro x -
    simpa using @hUs (i‚ÇÇ.1.base x) trivial
  ¬∑ rintro U
    refine H _ U.2 ((X'.restrictIsoOfEq ?_).hom ‚â´ i‚ÇÅ ‚à£_ _) (i‚ÇÇ ‚à£_ _) _ ?_
    ¬∑ exact congr($(h.1.1) ‚Åª¬π·µÅ U.1)
    ¬∑ rw [‚Üê (isPullback_morphismRestrict f U.1).paste_vert_iff]
      ¬∑ simp only [Scheme.restrictIsoOfEq, Category.assoc, morphismRestrict_Œπ,
          IsOpenImmersion.isoOfRangeEq_hom_fac_assoc]
        exact (isPullback_morphismRestrict f' (i‚ÇÇ ‚Åª¬π·µÅ U)).paste_vert h
      ¬∑ rw [‚Üê cancel_mono (Scheme.ŒπOpens _)]
        simp [IsOpenImmersion.isoOfRangeEq_hom_fac_assoc, Scheme.restrictIsoOfEq,
          morphismRestrict_Œπ_assoc, h.1.1]

#align algebraic_geometry.universally_is_local_at_target_of_morphism_restrict AlgebraicGeometry.universally_isLocalAtTarget

end Universally

section Topologically

/-- `topologically P` holds for a morphism if the underlying topological map satisfies `P`. -/
def MorphismProperty.topologically
    (P : ‚àÄ {Œ± Œ≤ : Type u} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] (_ : Œ± ‚Üí Œ≤), Prop) :
    MorphismProperty Scheme.{u} := fun _ _ f => P f.1.base
#align algebraic_geometry.morphism_property.topologically AlgebraicGeometry.MorphismProperty.topologically

variable (P : ‚àÄ {Œ± Œ≤ : Type u} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] (_ : Œ± ‚Üí Œ≤), Prop)

/-- If a property of maps of topological spaces is stable under composition, the induced
morphism property of schemes is stable under composition. -/
lemma MorphismProperty.topologically_isStableUnderComposition
    (hP : ‚àÄ {Œ± Œ≤ Œ≥ : Type u} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]
      (f : Œ± ‚Üí Œ≤) (g : Œ≤ ‚Üí Œ≥) (_ : P f) (_ : P g), P (g ‚àò f)) :
    (MorphismProperty.topologically P).IsStableUnderComposition where
  comp_mem {X Y Z} f g hf hg := by
    simp only [MorphismProperty.topologically, Scheme.comp_coeBase, TopCat.coe_comp]
    exact hP _ _ hf hg

/-- If a property of maps of topological spaces is satisfied by all homeomorphisms,
every isomorphism of schemes satisfies the induced property. -/
lemma MorphismProperty.topologically_iso_le
    (hP : ‚àÄ {Œ± Œ≤ : Type u} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] (f : Œ± ‚âÉ‚Çú Œ≤), P f) :
    MorphismProperty.isomorphisms Scheme ‚â§ (MorphismProperty.topologically P) := by
  intro X Y e (he : IsIso e)
  have : IsIso e := he
  exact hP (TopCat.homeoOfIso (asIso e.val.base))

/-- If a property of maps of topological spaces is satisfied by homeomorphisms and is stable
under composition, the induced property on schemes respects isomorphisms. -/
lemma MorphismProperty.topologically_respectsIso
    (hP‚ÇÅ : ‚àÄ {Œ± Œ≤ : Type u} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] (f : Œ± ‚âÉ‚Çú Œ≤), P f)
    (hP‚ÇÇ : ‚àÄ {Œ± Œ≤ Œ≥ : Type u} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]
      (f : Œ± ‚Üí Œ≤) (g : Œ≤ ‚Üí Œ≥) (_ : P f) (_ : P g), P (g ‚àò f)) :
      (MorphismProperty.topologically P).RespectsIso :=
  have : (MorphismProperty.topologically P).IsStableUnderComposition :=
    topologically_isStableUnderComposition P hP‚ÇÇ
  MorphismProperty.respectsIso_of_isStableUnderComposition (topologically_iso_le P hP‚ÇÅ)

/-- To check that a topologically defined morphism property is local at the target,
we may check the corresponding properties on topological spaces. -/
lemma MorphismProperty.topologically_propertyIsLocalAtTarget
    [(MorphismProperty.topologically P).RespectsIso]
    (hP‚ÇÇ : ‚àÄ {Œ± Œ≤ : Type u} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] (f : Œ± ‚Üí Œ≤) (s : Set Œ≤),
      P f ‚Üí P (s.restrictPreimage f))
    (hP‚ÇÉ : ‚àÄ {Œ± Œ≤ : Type u} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] (f : Œ± ‚Üí Œ≤) {Œπ : Type u}
      (U : Œπ ‚Üí TopologicalSpace.Opens Œ≤) (_ : iSup U = ‚ä§) (_ : Continuous f),
      (‚àÄ i, P ((U i).carrier.restrictPreimage f)) ‚Üí P f) :
    IsLocalAtTarget (MorphismProperty.topologically P) := by
  constructor
  ¬∑ infer_instance
  ¬∑ intro X Y f U hf
    simp_rw [MorphismProperty.topologically, morphismRestrict_val_base]
    exact hP‚ÇÇ f.val.base U.carrier hf
  ¬∑ intro X Y f Us hU hf
    apply hP‚ÇÉ f.val.base (fun i : Us ‚Ü¶ i.1) (by simpa using hU) f.val.base.continuous fun i ‚Ü¶ ?_
    rw [‚Üê morphismRestrict_val_base]
    exact hf i i.2

end Topologically

@[deprecated (since := "2024-06-22")]
alias diagonalTargetAffineLocallyOfOpenCover := HasAffineProperty.diagonal_of_openCover

@[deprecated (since := "2024-06-22")]
alias AffineTargetMorphismProperty.diagonalOfTargetAffineLocally :=
  HasAffineProperty.affineProperty_diagonal_of_diagonal_of_isPullback

@[deprecated (since := "2024-06-22")]
alias universallyIsLocalAtTarget := universally_isLocalAtTarget

@[deprecated (since := "2024-06-22")]
alias universallyIsLocalAtTargetOfMorphismRestrict :=
  universally_isLocalAtTarget

end AlgebraicGeometry
