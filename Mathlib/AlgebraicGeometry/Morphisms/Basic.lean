/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.AffineScheme
import Mathlib.AlgebraicGeometry.Pullbacks
import Mathlib.CategoryTheory.MorphismProperty.Local
import Mathlib.Data.List.TFAE

/-!
# Properties of morphisms between Schemes

We provide the basic framework for talking about properties of morphisms between Schemes.

A `MorphismProperty Scheme` is a predicate on morphisms between schemes. For properties local at
the target, its behaviour is entirely determined by its definition on morphisms into affine schemes,
which we call an `AffineTargetMorphismProperty`. In this file, we provide API lemmas for properties
local at the target, and special support for those properties whose `AffineTargetMorphismProperty`
takes on a more simple form. We also provide API lemmas for properties local at the target.
The main interfaces of the API are the typeclasses `IsLocalAtTarget`, `IsLocalAtSource` and
`HasAffineProperty`, which we describe in detail below.

## `IsZariskiLocalAtTarget`

- `AlgebraicGeometry.IsZariskiLocalAtTarget`: We say that `IsZariskiLocalAtTarget P` for
`P : MorphismProperty Scheme` if
1. `P` respects isomorphisms.
2. `P` holds for `f ‚à£_ U` for an open cover `U` of `Y` if and only if `P` holds for `f`.

For a morphism property `P` local at the target and `f : X ‚ü∂ Y`, we provide these API lemmas:

- `AlgebraicGeometry.IsZariskiLocalAtTarget.of_isPullback`:
    `P` is preserved under pullback along open immersions.
- `AlgebraicGeometry.IsZariskiLocalAtTarget.restrict`:
    `P f ‚Üí P (f ‚à£_ U)` for an open `U` of `Y`.
- `AlgebraicGeometry.IsZariskiLocalAtTarget.iff_of_iSup_eq_top`:
    `P f ‚Üî ‚àÄ i, P (f ‚à£_ U i)` for a family `U i` of open sets covering `Y`.
- `AlgebraicGeometry.IsZariskiLocalAtTarget.iff_of_openCover`:
    `P f ‚Üî ‚àÄ i, P (ùí∞.pullbackHom f i)` for `ùí∞ : Y.openCover`.

## `IsZariskiLocalAtSource`

- `AlgebraicGeometry.IsZariskiLocalAtSource`: We say that `IsZariskiLocalAtSource P` for
`P : MorphismProperty Scheme` if
1. `P` respects isomorphisms.
2. `P` holds for `ùí∞.map i ‚â´ f` for an open cover `ùí∞` of `X` iff `P` holds for `f : X ‚ü∂ Y`.

For a morphism property `P` local at the source and `f : X ‚ü∂ Y`, we provide these API lemmas:

- `AlgebraicGeometry.IsZariskiLocalAtSource.comp`:
    `P` is preserved under composition with open immersions at the source.
- `AlgebraicGeometry.IsZariskiLocalAtSource.iff_of_iSup_eq_top`:
    `P f ‚Üî ‚àÄ i, P (U.Œπ ‚â´ f)` for a family `U i` of open sets covering `X`.
- `AlgebraicGeometry.IsZariskiLocalAtSource.iff_of_openCover`:
    `P f ‚Üî ‚àÄ i, P (ùí∞.map i ‚â´ f)` for `ùí∞ : X.openCover`.
- `AlgebraicGeometry.IsZariskiLocalAtSource.of_isOpenImmersion`: If `P` contains identities then `P`
    holds for open immersions.

## `AffineTargetMorphismProperty`

- `AlgebraicGeometry.AffineTargetMorphismProperty`:
    The type of predicates on `f : X ‚ü∂ Y` with `Y` affine.
- `AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal`: We say that `P.IsLocal` if `P`
    satisfies the assumptions of the affine communication lemma
    (`AlgebraicGeometry.of_affine_open_cover`). That is,
    1. `P` respects isomorphisms.
    2. If `P` holds for `f : X ‚ü∂ Y`, then `P` holds for `f ‚à£_ Y.basicOpen r` for any
      global section `r`.
    3. If `P` holds for `f ‚à£_ Y.basicOpen r` for all `r` in a spanning set of the global sections,
      then `P` holds for `f`.

## `HasAffineProperty`

- `AlgebraicGeometry.HasAffineProperty`:
  `HasAffineProperty P Q` is a type class asserting that `P` is local at the target,
  and over affine schemes, it is equivalent to `Q : AffineTargetMorphismProperty`.

For `HasAffineProperty P Q` and `f : X ‚ü∂ Y`, we provide these API lemmas:

- `AlgebraicGeometry.HasAffineProperty.of_isPullback`:
    `P` is preserved under pullback along open immersions from affine schemes.
- `AlgebraicGeometry.HasAffineProperty.restrict`:
    `P f ‚Üí Q (f ‚à£_ U)` for affine `U` of `Y`.
- `AlgebraicGeometry.HasAffineProperty.iff_of_iSup_eq_top`:
    `P f ‚Üî ‚àÄ i, Q (f ‚à£_ U i)` for a family `U i` of affine open sets covering `Y`.
- `AlgebraicGeometry.HasAffineProperty.iff_of_openCover`:
    `P f ‚Üî ‚àÄ i, P (ùí∞.pullbackHom f i)` for affine open covers `ùí∞` of `Y`.
- `AlgebraicGeometry.HasAffineProperty.isStableUnderBaseChange`:
    If `Q` is stable under affine base change, then `P` is stable under arbitrary base change.

## Implementation details

The properties `IsZariskiLocalAtTarget` and `IsZariskiLocalAtSource` are defined as abbreviations
for the respective local property of morphism properties defined generally for categories equipped
with a `Precoverage`.
-/


universe u v

open TopologicalSpace CategoryTheory CategoryTheory.Limits Opposite

noncomputable section

namespace AlgebraicGeometry

/-- A property is Zariski-local at target if it is local at target in the Zariski topology. -/
abbrev IsZariskiLocalAtTarget (P : MorphismProperty Scheme.{u}) :=
  P.IsLocalAtTarget Scheme.zariskiPrecoverage

namespace IsZariskiLocalAtTarget

/--
`P` is local at the target if
1. `P` respects isomorphisms.
2. If `P` holds for `f : X ‚ü∂ Y`, then `P` holds for `f ‚à£_ U` for any `U`.
3. If `P` holds for `f ‚à£_ U` for an open cover `U` of `Y`, then `P` holds for `f`.
-/
protected lemma mk' {P : MorphismProperty Scheme} [P.RespectsIso]
    (restrict : ‚àÄ {X Y : Scheme} (f : X ‚ü∂ Y) (U : Y.Opens), P f ‚Üí P (f ‚à£_ U))
    (of_sSup_eq_top :
      ‚àÄ {X Y : Scheme.{u}} (f : X ‚ü∂ Y) {Œπ : Type u} (U : Œπ ‚Üí Y.Opens), iSup U = ‚ä§ ‚Üí
        (‚àÄ i, P (f ‚à£_ U i)) ‚Üí P f) :
    IsZariskiLocalAtTarget P where
  pullbackSnd ùí∞ i hf := (P.arrow_mk_iso_iff (morphismRestrictOpensRange _ _)).mp (restrict _ _ hf)
  of_zeroHypercover {X Y f} ùí∞ h := by
    refine of_sSup_eq_top f _ (Scheme.OpenCover.iSup_opensRange ùí∞) ?_
    exact fun i ‚Ü¶ (P.arrow_mk_iso_iff (morphismRestrictOpensRange f _)).mpr (h i)

variable {P : MorphismProperty Scheme.{u}} [IsZariskiLocalAtTarget P]
  {X Y : Scheme.{u}} {f : X ‚ü∂ Y} (ùí∞ : Y.OpenCover)

lemma of_isPullback {UX UY : Scheme.{u}} {iY : UY ‚ü∂ Y} [IsOpenImmersion iY]
    {iX : UX ‚ü∂ X} {f' : UX ‚ü∂ UY} (h : IsPullback iX f' f iY) (H : P f) : P f' :=
  MorphismProperty.IsLocalAtTarget.of_isPullback (Y.affineCover.add iY) .none h H

theorem restrict (hf : P f) (U : Y.Opens) : P (f ‚à£_ U) :=
  of_isPullback (isPullback_morphismRestrict f U).flip hf

lemma of_iSup_eq_top {Œπ} (U : Œπ ‚Üí Y.Opens) (hU : iSup U = ‚ä§)
    (H : ‚àÄ i, P (f ‚à£_ U i)) : P f := by
  refine (P.iff_of_zeroHypercover_target
    (Y.openCoverOfIsOpenCover (s := Set.range U) Subtype.val (by ext; simp [‚Üê hU]))).mpr fun i ‚Ü¶ ?_
  obtain ‚ü®_, i, rfl‚ü© := i
  refine (P.arrow_mk_iso_iff (morphismRestrictOpensRange f _)).mp ?_
  change P (f ‚à£_ (U i).Œπ.opensRange)
  rw [Scheme.Opens.opensRange_Œπ]
  exact H i

theorem iff_of_iSup_eq_top {Œπ} (U : Œπ ‚Üí Y.Opens) (hU : iSup U = ‚ä§) :
    P f ‚Üî ‚àÄ i, P (f ‚à£_ U i) :=
  ‚ü®fun H _ ‚Ü¶ restrict H _, of_iSup_eq_top U hU‚ü©

lemma of_openCover (H : ‚àÄ i, P (ùí∞.pullbackHom f i)) : P f := by
  apply of_iSup_eq_top (fun i ‚Ü¶ (ùí∞.f i).opensRange) ùí∞.iSup_opensRange
  exact fun i ‚Ü¶ (P.arrow_mk_iso_iff (morphismRestrictOpensRange f _)).mpr (H i)

theorem iff_of_openCover (ùí∞ : Y.OpenCover) :
    P f ‚Üî ‚àÄ i, P (ùí∞.pullbackHom f i) :=
  ‚ü®fun H _ ‚Ü¶ of_isPullback (.of_hasPullback _ _) H, of_openCover _‚ü©

lemma of_range_subset_iSup [P.RespectsRight @IsOpenImmersion] {Œπ : Type*} (U : Œπ ‚Üí Y.Opens)
    (H : Set.range f.base ‚äÜ (‚®Ü i, U i : Y.Opens)) (hf : ‚àÄ i, P (f ‚à£_ U i)) : P f := by
  let g : X ‚ü∂ (‚®Ü i, U i : Y.Opens) := IsOpenImmersion.lift (Scheme.Opens.Œπ _) f (by simpa using H)
  rw [‚Üê IsOpenImmersion.lift_fac (‚®Ü i, U i).Œπ f (by simpa using H)]
  apply MorphismProperty.RespectsRight.postcomp (Q := @IsOpenImmersion) _ inferInstance
  rw [iff_of_iSup_eq_top (P := P) (U := fun i : Œπ ‚Ü¶ (‚®Ü i, U i).Œπ ‚Åª¬π·µÅ U i)]
  ¬∑ intro i
    have heq : g ‚Åª¬π·µÅ (‚®Ü i, U i).Œπ ‚Åª¬π·µÅ U i = f ‚Åª¬π·µÅ U i := by
      change (g ‚â´ (‚®Ü i, U i).Œπ) ‚Åª¬π·µÅ U i = _
      simp [g]
    let e : Arrow.mk (g ‚à£_ (‚®Ü i, U i).Œπ ‚Åª¬π·µÅ U i) ‚âÖ Arrow.mk (f ‚à£_ U i) :=
        Arrow.isoMk (X.isoOfEq heq) (Scheme.Opens.isoOfLE (le_iSup U i)) <| by
      simp [‚Üê CategoryTheory.cancel_mono (U i).Œπ, g]
    rw [P.arrow_mk_iso_iff e]
    exact hf i
  apply (‚®Ü i, U i).Œπ.image_injective
  dsimp
  rw [Scheme.Hom.image_iSup, Scheme.Hom.image_top_eq_opensRange, Scheme.Opens.opensRange_Œπ]
  simp [Scheme.Hom.image_preimage_eq_opensRange_inf, le_iSup U]

end IsZariskiLocalAtTarget

/-- A property is Zariski-local at source if it is local at source in the Zariski topology. -/
abbrev IsZariskiLocalAtSource (P : MorphismProperty Scheme.{u}) :=
  P.IsLocalAtSource Scheme.zariskiPrecoverage

namespace IsZariskiLocalAtSource

/--
`P` is local at the source if
1. `P` respects isomorphisms.
2. If `P` holds for `f : X ‚ü∂ Y`, then `P` holds for `U.Œπ ‚â´ f` for any `U`.
3. If `P` holds for `U.Œπ ‚â´ f` for an open cover `U` of `X`, then `P` holds for `f`.
-/
protected lemma mk' {P : MorphismProperty Scheme} [P.RespectsIso]
    (restrict : ‚àÄ {X Y : Scheme} (f : X ‚ü∂ Y) (U : X.Opens), P f ‚Üí P (U.Œπ ‚â´ f))
    (of_sSup_eq_top :
      ‚àÄ {X Y : Scheme.{u}} (f : X ‚ü∂ Y) {Œπ : Type u} (U : Œπ ‚Üí X.Opens), iSup U = ‚ä§ ‚Üí
        (‚àÄ i, P ((U i).Œπ ‚â´ f)) ‚Üí P f) :
    IsZariskiLocalAtSource P where
  comp ùí∞ i H := by
    rw [‚Üê IsOpenImmersion.isoOfRangeEq_hom_fac (ùí∞.f i) (Scheme.Opens.Œπ _)
      (congr_arg Opens.carrier (ùí∞.f i).opensRange.opensRange_Œπ.symm), Category.assoc,
      P.cancel_left_of_respectsIso]
    exact restrict _ _ H
  of_zeroHypercover {X Y} f ùí∞ h := by
    refine of_sSup_eq_top f _ (Scheme.OpenCover.iSup_opensRange ùí∞) fun i ‚Ü¶ ?_
    rw [‚Üê IsOpenImmersion.isoOfRangeEq_inv_fac (ùí∞.f i) (Scheme.Opens.Œπ _)
      (congr_arg Opens.carrier (ùí∞.f i).opensRange.opensRange_Œπ.symm), Category.assoc,
      P.cancel_left_of_respectsIso]
    exact h _

variable {P : MorphismProperty Scheme.{u}} [IsZariskiLocalAtSource P]
variable {X Y : Scheme.{u}} {f : X ‚ü∂ Y} (ùí∞ : X.OpenCover)

lemma comp {UX : Scheme.{u}} (H : P f) (i : UX ‚ü∂ X) [IsOpenImmersion i] :
    P (i ‚â´ f) :=
  (P.iff_of_zeroHypercover_source (X.affineCover.add i)).mp H .none

/-- If `P` is local at the source, then it respects composition on the left with open immersions. -/
instance respectsLeft_isOpenImmersion {P : MorphismProperty Scheme}
    [IsZariskiLocalAtSource P] : P.RespectsLeft @IsOpenImmersion where
  precomp i _ _ hf := IsZariskiLocalAtSource.comp hf i

lemma of_iSup_eq_top {Œπ} (U : Œπ ‚Üí X.Opens) (hU : iSup U = ‚ä§)
    (H : ‚àÄ i, P ((U i).Œπ ‚â´ f)) : P f := by
  refine (P.iff_of_zeroHypercover_source
    (X.openCoverOfIsOpenCover (s := Set.range U) Subtype.val (by ext; simp [‚Üê hU]))).mpr fun i ‚Ü¶ ?_
  obtain ‚ü®_, i, rfl‚ü© := i
  exact H i

theorem iff_of_iSup_eq_top {Œπ} (U : Œπ ‚Üí X.Opens) (hU : iSup U = ‚ä§) :
    P f ‚Üî ‚àÄ i, P ((U i).Œπ ‚â´ f) :=
  ‚ü®fun H _ ‚Ü¶ comp H _, of_iSup_eq_top U hU‚ü©

lemma of_openCover (H : ‚àÄ i, P (ùí∞.f i ‚â´ f)) : P f := by
  refine of_iSup_eq_top (fun i ‚Ü¶ (ùí∞.f i).opensRange) ùí∞.iSup_opensRange fun i ‚Ü¶ ?_
  rw [‚Üê IsOpenImmersion.isoOfRangeEq_inv_fac (ùí∞.f i) (Scheme.Opens.Œπ _)
    (congr_arg Opens.carrier (ùí∞.f i).opensRange.opensRange_Œπ.symm), Category.assoc,
    P.cancel_left_of_respectsIso]
  exact H i

theorem iff_of_openCover :
    P f ‚Üî ‚àÄ i, P (ùí∞.f i ‚â´ f) :=
  ‚ü®fun H _ ‚Ü¶ comp H _, of_openCover _‚ü©

variable (f) in
lemma of_isOpenImmersion [P.ContainsIdentities] [IsOpenImmersion f] : P f :=
  Category.comp_id f ‚ñ∏ comp (P.id_mem Y) f

lemma isZariskiLocalAtTarget [P.IsMultiplicative]
    (hP : ‚àÄ {X Y Z : Scheme.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsOpenImmersion g], P (f ‚â´ g) ‚Üí P f) :
    IsZariskiLocalAtTarget P where
  pullbackSnd {X Y} f ùí∞ i hf := by
    apply hP _ (ùí∞.f i)
    rw [‚Üê pullback.condition]
    exact IsZariskiLocalAtSource.comp hf _
  of_zeroHypercover {X Y} f ùí∞ h := by
    rw [P.iff_of_zeroHypercover_source (ùí∞.pullback‚ÇÅ f)]
    intro i
    rw [‚Üê Scheme.Cover.pullbackHom_map]
    exact P.comp_mem _ _ (h i) (of_isOpenImmersion _)

lemma sigmaDesc {X : Scheme.{u}} {Œπ : Type v} [Small.{u} Œπ] {Y : Œπ ‚Üí Scheme.{u}}
    {f : ‚àÄ i, Y i ‚ü∂ X} (hf : ‚àÄ i, P (f i)) : P (Sigma.desc f) := by
  rw [IsZariskiLocalAtSource.iff_of_openCover (P := P) (Scheme.IsLocallyDirected.openCover _)]
  exact fun i ‚Ü¶ by simp [hf]

section IsZariskiLocalAtSourceAndTarget

/-- If `P` is local at the source and the target, then restriction on both source and target
preserves `P`. -/
lemma resLE [IsZariskiLocalAtTarget P] {U : Y.Opens} {V : X.Opens}
    (e : V ‚â§ f ‚Åª¬π·µÅ U)
    (hf : P f) : P (f.resLE U V e) :=
  IsZariskiLocalAtSource.comp (IsZariskiLocalAtTarget.restrict hf U) _

/-- If `P` is local at the source, local at the target and is stable under post-composition with
open immersions, then `P` can be checked locally around points. -/
lemma iff_exists_resLE [IsZariskiLocalAtTarget P]
    [P.RespectsRight @IsOpenImmersion] :
    P f ‚Üî ‚àÄ x : X, ‚àÉ (U : Y.Opens) (V : X.Opens) (_ : x ‚àà V.1) (e : V ‚â§ f ‚Åª¬π·µÅ U),
      P (f.resLE U V e) := by
  refine ‚ü®fun hf x ‚Ü¶ ‚ü®‚ä§, ‚ä§, trivial, by simp, resLE _ hf‚ü©, fun hf ‚Ü¶ ?_‚ü©
  choose U V hxU e hf using hf
  rw [IsZariskiLocalAtSource.iff_of_iSup_eq_top (fun x : X ‚Ü¶ V x) (P := P)]
  ¬∑ intro x
    rw [‚Üê Scheme.Hom.resLE_comp_Œπ _ (e x)]
    exact MorphismProperty.RespectsRight.postcomp (Q := @IsOpenImmersion) _ inferInstance _ (hf x)
  ¬∑ rw [eq_top_iff]
    rintro x -
    simp only [Opens.coe_iSup, Set.mem_iUnion, SetLike.mem_coe]
    use x, hxU x

end IsZariskiLocalAtSourceAndTarget

end IsZariskiLocalAtSource

/-- An `AffineTargetMorphismProperty` is a class of morphisms from an arbitrary scheme into an
affine scheme. -/
def AffineTargetMorphismProperty :=
  ‚àÄ ‚¶ÉX Y : Scheme‚¶Ñ (_ : X ‚ü∂ Y) [IsAffine Y], Prop

namespace AffineTargetMorphismProperty

@[ext]
lemma ext {P Q : AffineTargetMorphismProperty}
    (H : ‚àÄ ‚¶ÉX Y : Scheme‚¶Ñ (f : X ‚ü∂ Y) [IsAffine Y], P f ‚Üî Q f) : P = Q := by
  delta AffineTargetMorphismProperty; ext; exact H _

/-- The restriction of a `MorphismProperty Scheme` to morphisms with affine target. -/
def of (P : MorphismProperty Scheme) : AffineTargetMorphismProperty :=
  fun _ _ f _ ‚Ü¶ P f

/-- An `AffineTargetMorphismProperty` can be extended to a `MorphismProperty` such that it
*never* holds when the target is not affine -/
def toProperty (P : AffineTargetMorphismProperty) :
    MorphismProperty Scheme := fun _ _ f => ‚àÉ h, @P _ _ f h

theorem toProperty_apply (P : AffineTargetMorphismProperty)
    {X Y : Scheme} (f : X ‚ü∂ Y) [i : IsAffine Y] : P.toProperty f ‚Üî P f := by
  delta AffineTargetMorphismProperty.toProperty; simp [*]

theorem cancel_left_of_respectsIso
    (P : AffineTargetMorphismProperty) [P.toProperty.RespectsIso]
    {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsIso f] [IsAffine Z] : P (f ‚â´ g) ‚Üî P g := by
  rw [‚Üê P.toProperty_apply, ‚Üê P.toProperty_apply, P.toProperty.cancel_left_of_respectsIso]

theorem cancel_right_of_respectsIso
    (P : AffineTargetMorphismProperty) [P.toProperty.RespectsIso]
    {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsIso g] [IsAffine Z] [IsAffine Y] :
    P (f ‚â´ g) ‚Üî P f := by rw [‚Üê P.toProperty_apply, ‚Üê P.toProperty_apply,
      P.toProperty.cancel_right_of_respectsIso]

theorem arrow_mk_iso_iff
    (P : AffineTargetMorphismProperty) [P.toProperty.RespectsIso]
    {X Y X' Y' : Scheme} {f : X ‚ü∂ Y} {f' : X' ‚ü∂ Y'}
    (e : Arrow.mk f ‚âÖ Arrow.mk f') {h : IsAffine Y} :
    letI : IsAffine Y' := .of_isIso (Y := Y) e.inv.right
    P f ‚Üî P f' := by
  rw [‚Üê P.toProperty_apply, ‚Üê P.toProperty_apply, P.toProperty.arrow_mk_iso_iff e]

theorem respectsIso_mk {P : AffineTargetMorphismProperty}
    (h‚ÇÅ : ‚àÄ {X Y Z} (e : X ‚âÖ Y) (f : Y ‚ü∂ Z) [IsAffine Z], P f ‚Üí P (e.hom ‚â´ f))
    (h‚ÇÇ : ‚àÄ {X Y Z} (e : Y ‚âÖ Z) (f : X ‚ü∂ Y) [IsAffine Y],
      P f ‚Üí @P _ _ (f ‚â´ e.hom) (.of_isIso e.inv)) :
    P.toProperty.RespectsIso := by
  apply MorphismProperty.RespectsIso.mk
  ¬∑ rintro X Y Z e f ‚ü®a, h‚ü©; exact ‚ü®a, h‚ÇÅ e f h‚ü©
  ¬∑ rintro X Y Z e f ‚ü®a, h‚ü©; exact ‚ü®.of_isIso e.inv, h‚ÇÇ e f h‚ü©

instance respectsIso_of
    (P : MorphismProperty Scheme) [P.RespectsIso] :
    (of P).toProperty.RespectsIso := by
  apply respectsIso_mk
  ¬∑ intro _ _ _ _ _ _; apply MorphismProperty.RespectsIso.precomp
  ¬∑ intro _ _ _ _ _ _; apply MorphismProperty.RespectsIso.postcomp

/-- We say that `P : AffineTargetMorphismProperty` is a local property if
1. `P` respects isomorphisms.
2. If `P` holds for `f : X ‚ü∂ Y`, then `P` holds for `f ‚à£_ Y.basicOpen r` for any
  global section `r`.
3. If `P` holds for `f ‚à£_ Y.basicOpen r` for all `r` in a spanning set of the global sections,
  then `P` holds for `f`.
-/
class IsLocal (P : AffineTargetMorphismProperty) : Prop where
  /-- `P` as a morphism property respects isomorphisms -/
  respectsIso : P.toProperty.RespectsIso
  /-- `P` is stable under restriction to basic open set of global sections. -/
  to_basicOpen :
    ‚àÄ {X Y : Scheme} [IsAffine Y] (f : X ‚ü∂ Y) (r : Œì(Y, ‚ä§)), P f ‚Üí P (f ‚à£_ Y.basicOpen r)
  /-- `P` for `f` if `P` holds for `f` restricted to basic sets of a spanning set of the global
  sections -/
  of_basicOpenCover :
    ‚àÄ {X Y : Scheme} [IsAffine Y] (f : X ‚ü∂ Y) (s : Finset Œì(Y, ‚ä§))
      (_ : Ideal.span (s : Set Œì(Y, ‚ä§)) = ‚ä§), (‚àÄ r : s, P (f ‚à£_ Y.basicOpen r.1)) ‚Üí P f

attribute [instance] AffineTargetMorphismProperty.IsLocal.respectsIso

open AffineTargetMorphismProperty in
instance (P : MorphismProperty Scheme) [IsZariskiLocalAtTarget P] :
    (of P).IsLocal where
  respectsIso := inferInstance
  to_basicOpen _ _ H := IsZariskiLocalAtTarget.restrict H _
  of_basicOpenCover {_ Y} _ _ _ hs := IsZariskiLocalAtTarget.of_iSup_eq_top _
    ((isAffineOpen_top Y).iSup_basicOpen_eq_self_iff.mpr hs)

/-- A `P : AffineTargetMorphismProperty` is stable under base change if `P` holds for `Y ‚ü∂ S`
implies that `P` holds for `X √ó‚Çõ Y ‚ü∂ X` with `X` and `S` affine schemes. -/
def IsStableUnderBaseChange (P : AffineTargetMorphismProperty) : Prop :=
  ‚àÄ ‚¶ÉZ X Y S : Scheme‚¶Ñ [IsAffine S] [IsAffine X] {f : X ‚ü∂ S} {g : Y ‚ü∂ S}
    {f' : Z ‚ü∂ Y} {g' : Z ‚ü∂ X}, IsPullback g' f' f g ‚Üí P g ‚Üí P g'

lemma IsStableUnderBaseChange.mk (P : AffineTargetMorphismProperty) [P.toProperty.RespectsIso]
    (H : ‚àÄ ‚¶ÉX Y S : Scheme‚¶Ñ [IsAffine S] [IsAffine X] (f : X ‚ü∂ S) (g : Y ‚ü∂ S),
      P g ‚Üí P (pullback.fst f g)) : P.IsStableUnderBaseChange := by
  intro Z X Y S _ _ f g f' g' h hg
  rw [‚Üê P.cancel_left_of_respectsIso h.isoPullback.inv, h.isoPullback_inv_fst]
  exact H f g hg

end AffineTargetMorphismProperty

section targetAffineLocally

/-- For a `P : AffineTargetMorphismProperty`, `targetAffineLocally P` holds for
`f : X ‚ü∂ Y` whenever `P` holds for the restriction of `f` on every affine open subset of `Y`. -/
def targetAffineLocally (P : AffineTargetMorphismProperty) : MorphismProperty Scheme :=
  fun {X Y : Scheme} (f : X ‚ü∂ Y) => ‚àÄ U : Y.affineOpens, P (f ‚à£_ U)

theorem of_targetAffineLocally_of_isPullback
    {P : AffineTargetMorphismProperty} [P.IsLocal]
    {X Y UX UY : Scheme.{u}} [IsAffine UY] {f : X ‚ü∂ Y} {iY : UY ‚ü∂ Y} [IsOpenImmersion iY]
    {iX : UX ‚ü∂ X} {f' : UX ‚ü∂ UY} (h : IsPullback iX f' f iY) (hf : targetAffineLocally P f) :
    P f' := by
  rw [‚Üê P.cancel_left_of_respectsIso h.isoPullback.inv, h.isoPullback_inv_snd]
  exact (P.arrow_mk_iso_iff
    (morphismRestrictOpensRange f _)).mp (hf ‚ü®_, isAffineOpen_opensRange iY‚ü©)

instance (P : AffineTargetMorphismProperty) [P.toProperty.RespectsIso] :
    (targetAffineLocally P).RespectsIso := by
  apply MorphismProperty.RespectsIso.mk
  ¬∑ introv H U
    rw [morphismRestrict_comp, P.cancel_left_of_respectsIso]
    exact H U
  ¬∑ introv H
    rintro ‚ü®U, hU : IsAffineOpen U‚ü©; dsimp
    haveI : IsAffine _ := hU.preimage_of_isIso e.hom
    rw [morphismRestrict_comp, P.cancel_right_of_respectsIso]
    exact H ‚ü®(Opens.map e.hom.base).obj U, hU.preimage_of_isIso e.hom‚ü©

/--
`HasAffineProperty P Q` is a type class asserting that `P` is local at the target, and over affine
schemes, it is equivalent to `Q : AffineTargetMorphismProperty`.
To make the proofs easier, we state it instead as
1. `Q` is local at the target
2. `P f` if and only if `‚àÄ U, Q (f ‚à£_ U)` ranging over all affine opens of `U`.
See `HasAffineProperty.iff`.
-/
class HasAffineProperty (P : MorphismProperty Scheme)
    (Q : outParam AffineTargetMorphismProperty) : Prop where
  isLocal_affineProperty : Q.IsLocal
  eq_targetAffineLocally' : P = targetAffineLocally Q

namespace HasAffineProperty

variable (P : MorphismProperty Scheme) {Q} [HasAffineProperty P Q]
variable {X Y : Scheme.{u}} {f : X ‚ü∂ Y}

instance (Q : AffineTargetMorphismProperty) [Q.IsLocal] :
    HasAffineProperty (targetAffineLocally Q) Q :=
  ‚ü®inferInstance, rfl‚ü©

lemma eq_targetAffineLocally : P = targetAffineLocally Q := eq_targetAffineLocally'

/-- Every property local at the target can be associated with an affine target property.
This is not an instance as the associated property can often take on simpler forms. -/
lemma of_isZariskiLocalAtTarget (P : MorphismProperty Scheme.{u})
    [IsZariskiLocalAtTarget P] :
    HasAffineProperty P (AffineTargetMorphismProperty.of P) where
  isLocal_affineProperty := inferInstance
  eq_targetAffineLocally' := by
    ext X Y f
    constructor
    ¬∑ intro hf ‚ü®U, hU‚ü©
      exact IsZariskiLocalAtTarget.restrict hf _
    ¬∑ intro hf
      exact P.of_zeroHypercover_target Y.affineCover
        fun i ‚Ü¶ of_targetAffineLocally_of_isPullback (.of_hasPullback _ _) hf

@[deprecated (since := "2025-10-07")] alias of_isLocalAtTarget := of_isZariskiLocalAtTarget

lemma copy {P P'} {Q Q'} [HasAffineProperty P Q]
    (e : P = P') (e' : Q = Q') : HasAffineProperty P' Q' where
  isLocal_affineProperty := e' ‚ñ∏ isLocal_affineProperty P
  eq_targetAffineLocally' := e' ‚ñ∏ e.symm ‚ñ∏ eq_targetAffineLocally P

variable {P}

theorem of_isPullback {UX UY : Scheme.{u}} [IsAffine UY] {iY : UY ‚ü∂ Y} [IsOpenImmersion iY]
    {iX : UX ‚ü∂ X} {f' : UX ‚ü∂ UY} (h : IsPullback iX f' f iY) (hf : P f) :
    Q f' :=
  letI := isLocal_affineProperty P
  of_targetAffineLocally_of_isPullback h (eq_targetAffineLocally (P := P) ‚ñ∏ hf)

theorem restrict (hf : P f) (U : Y.affineOpens) :
    Q (f ‚à£_ U) :=
  of_isPullback (isPullback_morphismRestrict f U).flip hf

instance (priority := 900) : P.RespectsIso := by
  letI := isLocal_affineProperty P
  rw [eq_targetAffineLocally P]
  infer_instance

theorem of_iSup_eq_top
    {Œπ} (U : Œπ ‚Üí Y.affineOpens) (hU : ‚®Ü i, (U i : Y.Opens) = ‚ä§)
    (hU' : ‚àÄ i, Q (f ‚à£_ U i)) :
    P f := by
  letI := isLocal_affineProperty P
  rw [eq_targetAffineLocally P]
  classical
  intro V
  induction V using of_affine_open_cover U hU  with
  | basicOpen U r h =>
    haveI : IsAffine _ := U.2
    have := AffineTargetMorphismProperty.IsLocal.to_basicOpen (f ‚à£_ U.1) (U.1.topIso.inv r) h
    exact (Q.arrow_mk_iso_iff
      (morphismRestrictRestrictBasicOpen f _ r)).mp this
  | openCover U s hs H =>
    apply AffineTargetMorphismProperty.IsLocal.of_basicOpenCover _
      (s.image (Scheme.Opens.topIso _).inv) (by simp [‚Üê Ideal.map_span, hs, Ideal.map_top])
    intro ‚ü®r, hr‚ü©
    obtain ‚ü®r, hr', rfl‚ü© := Finset.mem_image.mp hr
    exact (Q.arrow_mk_iso_iff
      (morphismRestrictRestrictBasicOpen f _ r).symm).mp (H ‚ü®r, hr'‚ü©)
  | hU i => exact hU' i

theorem iff_of_iSup_eq_top
    {Œπ} (U : Œπ ‚Üí Y.affineOpens) (hU : ‚®Ü i, (U i : Y.Opens) = ‚ä§) :
    P f ‚Üî ‚àÄ i, Q (f ‚à£_ U i) :=
  ‚ü®fun H _ ‚Ü¶ restrict H _, fun H ‚Ü¶ HasAffineProperty.of_iSup_eq_top U hU H‚ü©

theorem of_openCover
    (ùí∞ : Y.OpenCover) [‚àÄ i, IsAffine (ùí∞.X i)] (hùí∞ : ‚àÄ i, Q (ùí∞.pullbackHom f i)) :
    P f :=
  letI := isLocal_affineProperty P
  of_iSup_eq_top
    (fun i ‚Ü¶ ‚ü®_, isAffineOpen_opensRange (ùí∞.f i)‚ü©) ùí∞.iSup_opensRange
    (fun i ‚Ü¶ (Q.arrow_mk_iso_iff (morphismRestrictOpensRange f _)).mpr (hùí∞ i))

theorem iff_of_openCover (ùí∞ : Y.OpenCover) [‚àÄ i, IsAffine (ùí∞.X i)] :
    P f ‚Üî ‚àÄ i, Q (ùí∞.pullbackHom f i) := by
  letI := isLocal_affineProperty P
  rw [iff_of_iSup_eq_top (P := P)
    (fun i ‚Ü¶ ‚ü®_, isAffineOpen_opensRange _‚ü©) ùí∞.iSup_opensRange]
  exact forall_congr' fun i ‚Ü¶ Q.arrow_mk_iso_iff
    (morphismRestrictOpensRange f _)

theorem iff_of_isAffine [IsAffine Y] : P f ‚Üî Q f := by
  letI := isLocal_affineProperty P
  haveI : ‚àÄ i, IsAffine ((Scheme.coverOfIsIso (P := @IsOpenImmersion) (ùüô Y)).X i) := fun i => by
    dsimp; infer_instance
  rw [iff_of_openCover (P := P) (Scheme.coverOfIsIso.{0} (ùüô Y))]
  trans Q (pullback.snd f (ùüô _))
  ¬∑ exact ‚ü®fun H => H PUnit.unit, fun H _ => H‚ü©
  rw [‚Üê Category.comp_id (pullback.snd _ _), ‚Üê pullback.condition,
    Q.cancel_left_of_respectsIso]

instance (priority := 900) : IsZariskiLocalAtTarget P := by
  letI := isLocal_affineProperty P
  apply IsZariskiLocalAtTarget.mk'
  ¬∑ rw [eq_targetAffineLocally P]
    intro X Y f U H V
    rw [Q.arrow_mk_iso_iff (morphismRestrictRestrict f _ _)]
    exact H ‚ü®_, V.2.image_of_isOpenImmersion (Y.ofRestrict _)‚ü©
  ¬∑ rintro X Y f Œπ U hU H
    let ùí∞ := Y.openCoverOfIsOpenCover U hU
    apply of_openCover ùí∞.affineRefinement.openCover
    rintro ‚ü®i, j‚ü©
    have : P (ùí∞.pullbackHom f i) := by
      refine (P.arrow_mk_iso_iff
        (morphismRestrictEq _ ?_ ‚â™‚â´ morphismRestrictOpensRange f (ùí∞.f i))).mp (H i)
      exact (Scheme.Opens.opensRange_Œπ _).symm
    rw [‚Üê Q.cancel_left_of_respectsIso (ùí∞.pullbackCoverAffineRefinementObjIso f _).inv,
      ùí∞.pullbackCoverAffineRefinementObjIso_inv_pullbackHom]
    exact of_isPullback (.of_hasPullback _ _) this

open AffineTargetMorphismProperty in
protected theorem iff {P : MorphismProperty Scheme} {Q : AffineTargetMorphismProperty} :
    HasAffineProperty P Q ‚Üî IsZariskiLocalAtTarget P ‚àß Q = of P :=
  ‚ü®fun _ ‚Ü¶ ‚ü®inferInstance, ext fun _ _ _ ‚Ü¶ iff_of_isAffine.symm‚ü©,
    fun ‚ü®_, e‚ü© ‚Ü¶ e ‚ñ∏ of_isZariskiLocalAtTarget P‚ü©

private theorem pullback_fst_of_right (hP' : Q.IsStableUnderBaseChange)
    {X Y S : Scheme} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [IsAffine S] (H : Q g) :
    P (pullback.fst f g) := by
  letI := isLocal_affineProperty P
  rw [iff_of_openCover (P := P) X.affineCover]
  intro i
  let e := pullbackSymmetry _ _ ‚â™‚â´ pullbackRightPullbackFstIso f g (X.affineCover.f i)
  have : e.hom ‚â´ pullback.fst _ _ = X.affineCover.pullbackHom (pullback.fst _ _) i := by
    simp [e, Scheme.Cover.pullbackHom]
  rw [‚Üê this, Q.cancel_left_of_respectsIso]
  apply hP' (.of_hasPullback _ _)
  exact H

theorem isStableUnderBaseChange (hP' : Q.IsStableUnderBaseChange) :
    P.IsStableUnderBaseChange :=
  MorphismProperty.IsStableUnderBaseChange.mk'
    (fun X Y S f g _ H => by
      rw [P.iff_of_zeroHypercover_target (S.affineCover.pullback‚ÇÅ f)]
      intro i
      let e : pullback (pullback.fst f g) ((S.affineCover.pullback‚ÇÅ f).f i) ‚âÖ
          _ := by
        refine pullbackSymmetry _ _ ‚â™‚â´ pullbackRightPullbackFstIso f g _ ‚â™‚â´ ?_ ‚â™‚â´
          (pullbackRightPullbackFstIso (S.affineCover.f i) g
            (pullback.snd f (S.affineCover.f i))).symm
        exact asIso
          (pullback.map _ _ _ _ (ùüô _) (ùüô _) (ùüô _) (by simpa using pullback.condition) (by simp))
      have : e.hom ‚â´ pullback.fst _ _ =
          pullback.snd (pullback.fst f g) ((S.affineCover.pullback‚ÇÅ f).f i) := by
        simp [e]
      rw [‚Üê this, P.cancel_left_of_respectsIso]
      apply HasAffineProperty.pullback_fst_of_right hP'
      letI := isLocal_affineProperty P
      rw [‚Üê pullbackSymmetry_hom_comp_snd, Q.cancel_left_of_respectsIso]
      apply of_isPullback (.of_hasPullback _ _) H)

lemma isZariskiLocalAtSource
    (H : ‚àÄ {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [IsAffine Y] (ùí∞ : Scheme.OpenCover.{u} X),
        Q f ‚Üî ‚àÄ i, Q (ùí∞.f i ‚â´ f)) : IsZariskiLocalAtSource P := by
  refine .mk_of_iff ?_
  intro X Y f ùí∞
  simp_rw [IsZariskiLocalAtTarget.iff_of_iSup_eq_top _ (iSup_affineOpens_eq_top Y)]
  rw [forall_comm]
  refine forall_congr' fun U ‚Ü¶ ?_
  simp_rw [HasAffineProperty.iff_of_isAffine, morphismRestrict_comp]
  exact @H _ _ (f ‚à£_ U.1) U.2 (Scheme.OpenCover.restrict ùí∞ (f ‚Åª¬π·µÅ U.1))

end HasAffineProperty

end targetAffineLocally

open MorphismProperty

lemma hasOfPostcompProperty_isOpenImmersion_of_morphismRestrict (P : MorphismProperty Scheme)
    [P.RespectsIso] (H : ‚àÄ {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : Y.Opens), P f ‚Üí P (f ‚à£_ U)) :
    P.HasOfPostcompProperty @IsOpenImmersion where
  of_postcomp {X Y Z} f g hg hfg := by
    have : (f ‚â´ g) ‚Åª¬π·µÅ g.opensRange = ‚ä§ := by simp
    have : f = X.topIso.inv ‚â´ (X.isoOfEq this).inv ‚â´ (f ‚â´ g) ‚à£_ g.opensRange ‚â´
        (IsOpenImmersion.isoOfRangeEq g.opensRange.Œπ g (by simp)).hom := by
      simp [‚Üê cancel_mono g]
    simp_rw [this, cancel_left_of_respectsIso (P := P), cancel_right_of_respectsIso (P := P)]
    exact H _ _ hfg

instance (P : MorphismProperty Scheme) [P.IsStableUnderBaseChange] :
    P.HasOfPostcompProperty @IsOpenImmersion :=
  HasOfPostcompProperty.of_le P (.monomorphisms Scheme) (fun _ _ f _ ‚Ü¶ inferInstanceAs (Mono f))

section Deprecations

@[deprecated (since := "2025-10-07")] alias IsLocalAtTarget := IsZariskiLocalAtTarget

namespace IsLocalAtTarget

@[deprecated (since := "2025-10-07")]
alias mk' := IsZariskiLocalAtTarget.mk'

@[deprecated (since := "2025-10-07")]
alias of_iSup_eq_top := IsZariskiLocalAtTarget.of_iSup_eq_top

@[deprecated (since := "2025-10-07")]
alias iff_of_iSup_eq_top := IsZariskiLocalAtTarget.iff_of_iSup_eq_top

@[deprecated (since := "2025-10-07")]
alias of_openCover := IsZariskiLocalAtTarget.of_openCover

@[deprecated (since := "2025-10-07")]
alias iff_of_openCover := IsZariskiLocalAtTarget.iff_of_openCover

@[deprecated (since := "2025-10-07")]
alias of_isPullback := IsZariskiLocalAtTarget.of_isPullback

@[deprecated (since := "2025-10-07")]
alias restrict := IsZariskiLocalAtTarget.restrict

@[deprecated (since := "2025-10-07")]
alias of_range_subset_iSup := IsZariskiLocalAtTarget.of_range_subset_iSup

end IsLocalAtTarget

@[deprecated (since := "2025-10-07")] alias IsLocalAtSource := IsZariskiLocalAtSource

namespace IsLocalAtSource

@[deprecated (since := "2025-10-07")]
alias mk' := IsZariskiLocalAtSource.mk'

@[deprecated (since := "2025-10-07")]
alias comp := IsZariskiLocalAtSource.comp

@[deprecated (since := "2025-10-07")]
alias respectsLeft_isOpenImmersion := IsZariskiLocalAtSource.respectsLeft_isOpenImmersion

@[deprecated (since := "2025-10-07")]
alias of_iSup_eq_top := IsZariskiLocalAtSource.of_iSup_eq_top

@[deprecated (since := "2025-10-07")]
alias iff_of_iSup_eq_top := IsZariskiLocalAtSource.iff_of_iSup_eq_top

@[deprecated (since := "2025-10-07")]
alias of_openCover := IsZariskiLocalAtSource.of_openCover

@[deprecated (since := "2025-10-07")]
alias iff_of_openCover := IsZariskiLocalAtSource.iff_of_openCover

@[deprecated (since := "2025-10-07")]
alias of_isOpenImmersion := IsZariskiLocalAtSource.of_isOpenImmersion

@[deprecated (since := "2025-10-07")]
alias isLocalAtTarget := IsZariskiLocalAtSource.isZariskiLocalAtTarget

@[deprecated (since := "2025-10-07")]
alias sigmaDesc := IsZariskiLocalAtSource.sigmaDesc

@[deprecated (since := "2025-10-07")]
alias resLE := IsZariskiLocalAtSource.resLE

@[deprecated (since := "2025-10-07")]
alias iff_exists_resLE := IsZariskiLocalAtSource.iff_exists_resLE

end IsLocalAtSource

end Deprecations

end AlgebraicGeometry
