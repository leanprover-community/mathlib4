/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.AffineScheme
import Mathlib.AlgebraicGeometry.Pullbacks
import Mathlib.CategoryTheory.MorphismProperty.Limits
import Mathlib.Data.List.TFAE

#align_import algebraic_geometry.morphisms.basic from "leanprover-community/mathlib"@"434e2fd21c1900747afc6d13d8be7f4eedba7218"

/-!
# Properties of morphisms between Schemes

We provide the basic framework for talking about properties of morphisms between Schemes.

A `MorphismProperty Scheme` is a predicate on morphisms between schemes, and an
`AffineTargetMorphismProperty` is a predicate on morphisms into affine schemes. Given a
`P : AffineTargetMorphismProperty`, we may construct a `MorphismProperty` called
`targetAffineLocally P` that holds for `f : X âŸ¶ Y` whenever `P` holds for the
restriction of `f` on every affine open subset of `Y`.

## Main definitions

- `AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal`: We say that `P.IsLocal` if `P`
satisfies the assumptions of the affine communication lemma
(`AlgebraicGeometry.of_affine_open_cover`). That is,
1. `P` respects isomorphisms.
2. If `P` holds for `f : X âŸ¶ Y`, then `P` holds for `f âˆ£_ Y.basicOpen r` for any
  global section `r`.
3. If `P` holds for `f âˆ£_ Y.basicOpen r` for all `r` in a spanning set of the global sections,
  then `P` holds for `f`.

- `AlgebraicGeometry.PropertyIsLocalAtTarget`: We say that `PropertyIsLocalAtTarget P` for
`P : MorphismProperty Scheme` if
1. `P` respects isomorphisms.
2. If `P` holds for `f : X âŸ¶ Y`, then `P` holds for `f âˆ£_ U` for any `U`.
3. If `P` holds for `f âˆ£_ U` for an open cover `U` of `Y`, then `P` holds for `f`.

## Main results

- `AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal.affine_openCover_TFAE`:
  If `P.IsLocal`, then `targetAffineLocally P f` iff there exists an affine cover `{ Uáµ¢ }` of `Y`
  such that `P` holds for `f âˆ£_ Uáµ¢`.
- `AlgebraicGeometry.AffineTargetMorphismProperty.isLocalOfOpenCoverImply`:
  If the existence of an affine cover `{ Uáµ¢ }` of `Y` such that `P` holds for `f âˆ£_ Uáµ¢` implies
  `targetAffineLocally P f`, then `P.IsLocal`.
- `AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal.affine_target_iff`:
  If `Y` is affine and `f : X âŸ¶ Y`, then `targetAffineLocally P f â†” P f` provided `P.IsLocal`.
- `AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal.targetAffineLocally_isLocal` :
  If `P.IsLocal`, then `PropertyIsLocalAtTarget (targetAffineLocally P)`.
- `AlgebraicGeometry.PropertyIsLocalAtTarget.openCover_TFAE`:
  If `PropertyIsLocalAtTarget P`, then `P f` iff there exists an open cover `{ Uáµ¢ }` of `Y`
  such that `P` holds for `f âˆ£_ Uáµ¢`.

These results should not be used directly, and should be ported to each property that is local.

-/

set_option linter.uppercaseLean3 false

universe u

open TopologicalSpace CategoryTheory CategoryTheory.Limits Opposite

noncomputable section

namespace AlgebraicGeometry

section PropertyIsLocalAtTarget

/--
We say that `P : MorphismProperty Scheme` is local at the target if
1. `P` respects isomorphisms.
2. If `P` holds for `f : X âŸ¶ Y`, then `P` holds for `f âˆ£_ U` for any `U`.
3. If `P` holds for `f âˆ£_ U` for an open cover `U` of `Y`, then `P` holds for `f`.
-/
structure PropertyIsLocalAtTarget (P : MorphismProperty Scheme) : Prop where
  /-- `P` respects isomorphisms. -/
  RespectsIso : P.RespectsIso
  /-- If `P` holds for `f : X âŸ¶ Y`, then `P` holds for `f âˆ£_ U` for any `U`. -/
  restrict : âˆ€ {X Y : Scheme} (f : X âŸ¶ Y) (U : Opens Y), P f â†’ P (f âˆ£_ U)
  /-- If `P` holds for `f âˆ£_ U` for an open cover `U` of `Y`, then `P` holds for `f`.  -/
  of_sSup_eq_top :
    âˆ€ {X Y : Scheme.{u}} (f : X âŸ¶ Y) (Us : Set (Opens Y)), sSup Us = âŠ¤ â†’
      (âˆ€ U âˆˆ Us, P (f âˆ£_ U)) â†’ P f
#align algebraic_geometry.property_is_local_at_target AlgebraicGeometry.PropertyIsLocalAtTarget

variable {P} (hP : PropertyIsLocalAtTarget P)
variable {X Y U : Scheme.{u}} (f : X âŸ¶ Y) (g : U âŸ¶ Y) [IsOpenImmersion g] (ğ’° : Y.OpenCover)

lemma PropertyIsLocalAtTarget.baseChange (H : P f) :
    P (pullback.snd : pullback f g âŸ¶ U) :=
  (hP.RespectsIso.arrow_mk_iso_iff
    (morphismRestrictOpensRange f _)).mp (hP.restrict f g.opensRange H)

lemma PropertyIsLocalAtTarget.of_iSup_eq_top {Î¹} (U : Î¹ â†’ Opens Y) (hU : iSup U = âŠ¤)
    (H : âˆ€ i, P (f âˆ£_ U i)) : P f :=
  hP.of_sSup_eq_top f (Set.range U) hU (fun _ âŸ¨j, eâŸ© â†¦ e â–¸ H j)

theorem PropertyIsLocalAtTarget.iff_of_iSup_eq_top {Î¹} (U : Î¹ â†’ Opens Y) (hU : iSup U = âŠ¤) :
    P f â†” âˆ€ i, P (f âˆ£_ U i) :=
  âŸ¨fun H _ â†¦ hP.restrict _ _ H, of_iSup_eq_top hP _ U hUâŸ©

lemma PropertyIsLocalAtTarget.of_openCover (H : âˆ€ i, P (ğ’°.pullbackHom f i)) : P f := by
  apply hP.of_iSup_eq_top f (fun i â†¦ (ğ’°.map i).opensRange) ğ’°.iSup_opensRange
  exact fun i â†¦ (hP.RespectsIso.arrow_mk_iso_iff (morphismRestrictOpensRange f _)).mpr (H i)

theorem PropertyIsLocalAtTarget.iff_of_openCover (ğ’° : Y.OpenCover) :
    P f â†” âˆ€ i, P (ğ’°.pullbackHom f i) :=
  âŸ¨fun H _ â†¦ hP.baseChange _ _ H, of_openCover hP _ _âŸ©

end PropertyIsLocalAtTarget

section AffineTargetMorphismProperty

/-- An `AffineTargetMorphismProperty` is a class of morphisms from an arbitrary scheme into an
affine scheme. -/
def AffineTargetMorphismProperty :=
  âˆ€ â¦ƒX Y : Schemeâ¦„ (_ : X âŸ¶ Y) [IsAffine Y], Prop
#align algebraic_geometry.affine_target_morphism_property AlgebraicGeometry.AffineTargetMorphismProperty

/-- `IsIso` as a `MorphismProperty`. -/
protected def Scheme.isIso : MorphismProperty Scheme :=
  @IsIso Scheme _
#align algebraic_geometry.Scheme.is_iso AlgebraicGeometry.Scheme.isIso

/-- `IsIso` as an `AffineTargetMorphismProperty`. -/
protected def Scheme.affineTargetIsIso : AffineTargetMorphismProperty := fun _ _ f _ => IsIso f
#align algebraic_geometry.Scheme.affine_target_is_iso AlgebraicGeometry.Scheme.affineTargetIsIso

instance : Inhabited AffineTargetMorphismProperty := âŸ¨Scheme.affineTargetIsIsoâŸ©

/-- An `AffineTargetMorphismProperty` can be extended to a `MorphismProperty` such that it
*never* holds when the target is not affine -/
def AffineTargetMorphismProperty.toProperty (P : AffineTargetMorphismProperty) :
    MorphismProperty Scheme := fun _ _ f => âˆƒ h, @P _ _ f h
#align algebraic_geometry.affine_target_morphism_property.to_property AlgebraicGeometry.AffineTargetMorphismProperty.toProperty

theorem AffineTargetMorphismProperty.toProperty_apply (P : AffineTargetMorphismProperty)
    {X Y : Scheme} (f : X âŸ¶ Y) [i : IsAffine Y] : P.toProperty f â†” P f := by
  delta AffineTargetMorphismProperty.toProperty; simp [*]
#align algebraic_geometry.affine_target_morphism_property.to_property_apply AlgebraicGeometry.AffineTargetMorphismProperty.toProperty_apply

theorem affine_cancel_left_isIso {P : AffineTargetMorphismProperty} (hP : P.toProperty.RespectsIso)
    {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [IsIso f] [IsAffine Z] : P (f â‰« g) â†” P g := by
  rw [â† P.toProperty_apply, â† P.toProperty_apply, hP.cancel_left_isIso]
#align algebraic_geometry.affine_cancel_left_is_iso AlgebraicGeometry.affine_cancel_left_isIso

theorem affine_cancel_right_isIso {P : AffineTargetMorphismProperty} (hP : P.toProperty.RespectsIso)
    {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [IsIso g] [IsAffine Z] [IsAffine Y] :
    P (f â‰« g) â†” P f := by rw [â† P.toProperty_apply, â† P.toProperty_apply, hP.cancel_right_isIso]
#align algebraic_geometry.affine_cancel_right_is_iso AlgebraicGeometry.affine_cancel_right_isIso

theorem affine_arrow_mk_iso_iff {P : AffineTargetMorphismProperty} (hP : P.toProperty.RespectsIso)
    {X Y X' Y' : Scheme} {f : X âŸ¶ Y} {f' : X' âŸ¶ Y'}
    (e : Arrow.mk f â‰… Arrow.mk f') {h : IsAffine Y} :
    letI : IsAffine Y' := isAffine_of_isIso (Y := Y) e.inv.right
    P f â†” P f' := by
  rw [â† P.toProperty_apply, â† P.toProperty_apply, hP.arrow_mk_iso_iff e]

theorem AffineTargetMorphismProperty.respectsIso_mk {P : AffineTargetMorphismProperty}
    (hâ‚ : âˆ€ {X Y Z} (e : X â‰… Y) (f : Y âŸ¶ Z) [IsAffine Z], P f â†’ P (e.hom â‰« f))
    (hâ‚‚ : âˆ€ {X Y Z} (e : Y â‰… Z) (f : X âŸ¶ Y) [h : IsAffine Y],
      P f â†’ @P _ _ (f â‰« e.hom) (isAffine_of_isIso e.inv)) :
    P.toProperty.RespectsIso := by
  constructor
  Â· rintro X Y Z e f âŸ¨a, hâŸ©; exact âŸ¨a, hâ‚ e f hâŸ©
  Â· rintro X Y Z e f âŸ¨a, hâŸ©; exact âŸ¨isAffine_of_isIso e.inv, hâ‚‚ e f hâŸ©
#align algebraic_geometry.affine_target_morphism_property.respects_iso_mk AlgebraicGeometry.AffineTargetMorphismProperty.respectsIso_mk

/-- We say that `P : AffineTargetMorphismProperty` is a local property if
1. `P` respects isomorphisms.
2. If `P` holds for `f : X âŸ¶ Y`, then `P` holds for `f âˆ£_ Y.basicOpen r` for any
  global section `r`.
3. If `P` holds for `f âˆ£_ Y.basicOpen r` for all `r` in a spanning set of the global sections,
  then `P` holds for `f`.
-/
structure AffineTargetMorphismProperty.IsLocal (P : AffineTargetMorphismProperty) : Prop where
  /-- `P` as a morphism property respects isomorphisms -/
  respectsIso : P.toProperty.RespectsIso
  /-- `P` is stable under restriction to basic open set of global sections. -/
  to_basicOpen :
    âˆ€ {X Y : Scheme} [IsAffine Y] (f : X âŸ¶ Y) (r : Î“(Y, âŠ¤)),
      P f â†’ @P _ _ (f âˆ£_ Y.basicOpen r) ((isAffineOpen_top Y).basicOpen _)
  /-- `P` for `f` if `P` holds for `f` restricted to basic sets of a spanning set of the global
    sections -/
  of_basicOpenCover :
    âˆ€ {X Y : Scheme} [IsAffine Y] (f : X âŸ¶ Y) (s : Finset Î“(Y, âŠ¤))
      (_ : Ideal.span (s : Set Î“(Y, âŠ¤)) = âŠ¤),
      (âˆ€ r : s, @P _ _ (f âˆ£_ Y.basicOpen r.1) ((isAffineOpen_top Y).basicOpen _)) â†’ P f
#align algebraic_geometry.affine_target_morphism_property.is_local AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal

end AffineTargetMorphismProperty

section targetAffineLocally

/-- For a `P : AffineTargetMorphismProperty`, `targetAffineLocally P` holds for
`f : X âŸ¶ Y` whenever `P` holds for the restriction of `f` on every affine open subset of `Y`. -/
def targetAffineLocally (P : AffineTargetMorphismProperty) : MorphismProperty Scheme :=
  fun {X Y : Scheme} (f : X âŸ¶ Y) => âˆ€ U : Y.affineOpens, P (f âˆ£_ U)
#align algebraic_geometry.target_affine_locally AlgebraicGeometry.targetAffineLocally

variable {P : AffineTargetMorphismProperty} (hP : P.IsLocal) {X Y : Scheme.{u}} (f : X âŸ¶ Y)

theorem IsAffineOpen.preimage_of_isIso {X Y : Scheme} {U : Opens Y.carrier} (hU : IsAffineOpen U)
    (f : X âŸ¶ Y) [IsIso f] : IsAffineOpen (f â»Â¹áµ U) :=
  haveI : IsAffine _ := hU
  isAffine_of_isIso (f âˆ£_ U)
#align algebraic_geometry.is_affine_open.map_is_iso AlgebraicGeometry.IsAffineOpen.preimage_of_isIso

theorem targetAffineLocally_respectsIso (hP : P.toProperty.RespectsIso) :
    (targetAffineLocally P).RespectsIso := by
  constructor
  Â· introv H U
    rw [morphismRestrict_comp, affine_cancel_left_isIso hP]
    exact H U
  Â· introv H
    rintro âŸ¨U, hU : IsAffineOpen UâŸ©; dsimp
    haveI : IsAffine _ := hU.preimage_of_isIso e.hom
    rw [morphismRestrict_comp, affine_cancel_right_isIso hP]
    exact H âŸ¨(Opens.map e.hom.val.base).obj U, hU.preimage_of_isIso e.homâŸ©
#align algebraic_geometry.target_affine_locally_respects_iso AlgebraicGeometry.targetAffineLocally_respectsIso

/-- Specialization of `ConcreteCategory.id_apply` because `simp` can't see through the defeq. -/
@[local simp] lemma CommRingCat.id_apply (R : CommRingCat) (x : R) : ğŸ™ R x = x := rfl

attribute [-simp] eqToHom_op in
@[simps!]
def Scheme.resTop (X : Scheme.{u}) (U : Opens X) : Î“(X âˆ£_áµ¤ U, âŠ¤) â‰… Î“(X, U) :=
  X.presheaf.mapIso (eqToIso U.openEmbedding_obj_top.symm).op

theorem targetAffineLocally_of_iSup_eq_top
    {Î¹} (U : Î¹ â†’ Y.affineOpens) (hU : â¨† i, (U i : Opens Y) = âŠ¤) (hU' : âˆ€ i, P (f âˆ£_ U i)) :
    targetAffineLocally P f := by
  classical
  intro V
  induction V using of_affine_open_cover U hU  with
  | basicOpen U r h =>
    haveI : IsAffine _ := U.2
    have := hP.2 (f âˆ£_ U.1)
    replace this := this ((Y.resTop U).inv r) h
    rw [â† P.toProperty_apply] at this âŠ¢
    exact (hP.1.arrow_mk_iso_iff (morphismRestrictRestrictBasicOpen f _ r)).mp this
  | openCover U s hs H =>
    apply hP.3 _ (s.image (Y.resTop _).inv) (by simp [â† Ideal.map_span, hs, Ideal.map_top])
    intro âŸ¨r, hrâŸ©
    obtain âŸ¨r, hr', rflâŸ© := Finset.mem_image.mp hr
    exact (affine_arrow_mk_iso_iff hP.1
      (morphismRestrictRestrictBasicOpen f _ r).symm).mp (H âŸ¨r, hr'âŸ©)
  | hU i => exact hU' i

theorem targetAffineLocally_iff_of_iSup_eq_top
    {Î¹} (U : Î¹ â†’ Y.affineOpens) (hU : â¨† i, (U i : Opens Y) = âŠ¤) :
    targetAffineLocally P f â†” âˆ€ i, P (f âˆ£_ U i) :=
  âŸ¨fun H _ â†¦ H _, fun H â†¦ targetAffineLocally_of_iSup_eq_top hP f U hU HâŸ©

theorem targetAffineLocally_of_openCover
    (ğ’° : Y.OpenCover) [âˆ€ i, IsAffine (ğ’°.obj i)] (hğ’° : âˆ€ i, P (ğ’°.pullbackHom f i)) :
    targetAffineLocally P f := by
  apply targetAffineLocally_of_iSup_eq_top hP f
    (fun i â†¦ âŸ¨_, isAffineOpen_opensRange (ğ’°.map i)âŸ©) ğ’°.iSup_opensRange
    (fun i â†¦ (affine_arrow_mk_iso_iff hP.1 (morphismRestrictOpensRange f _)).mpr (hğ’° i))
#align algebraic_geometry.target_affine_locally_of_open_cover AlgebraicGeometry.targetAffineLocally_of_openCover

theorem targetAffineLocally_iff_of_openCover
    (ğ’° : Y.OpenCover) [âˆ€ i, IsAffine (ğ’°.obj i)] :
    targetAffineLocally P f â†” âˆ€ i, P (ğ’°.pullbackHom f i) := by
  rw [targetAffineLocally_iff_of_iSup_eq_top hP f
    (fun i â†¦ âŸ¨_, isAffineOpen_opensRange _âŸ©) ğ’°.iSup_opensRange]
  exact forall_congr' fun i â†¦ affine_arrow_mk_iso_iff hP.1 (morphismRestrictOpensRange f _)

theorem targetAffineLocally_morphsimRestrict
    (U : Opens Y) (H : targetAffineLocally P f) :
    targetAffineLocally P (f âˆ£_ U) := by
  intros V
  rw [affine_arrow_mk_iso_iff hP.respectsIso (morphismRestrictRestrict _ _ _)]
  exact H âŸ¨_, V.2.image_of_isOpenImmersion _âŸ©

theorem targetAffineLocally_pullback {U} (g : U âŸ¶ Y) [IsOpenImmersion g]
    (h : targetAffineLocally P f) :
    targetAffineLocally P (pullback.snd : pullback f g âŸ¶ _) := by
  rw [â† (targetAffineLocally_respectsIso hP.1).arrow_mk_iso_iff (morphismRestrictOpensRange _ _)]
  exact targetAffineLocally_morphsimRestrict hP f _ h

theorem AffineTargetMorphismProperty.isLocalOfOpenCoverImply (P : AffineTargetMorphismProperty)
    (hP : P.toProperty.RespectsIso)
    (H : âˆ€ {X Y : Scheme.{u}} (f : X âŸ¶ Y),
      (âˆƒ (ğ’° : Scheme.OpenCover.{u} Y) (_ : âˆ€ i, IsAffine (ğ’°.obj i)),
        âˆ€ i : ğ’°.J, P (pullback.snd : (ğ’°.pullbackCover f).obj i âŸ¶ ğ’°.obj i)) â†’
        âˆ€ {U : Scheme} (g : U âŸ¶ Y) [IsAffine U] [IsOpenImmersion g],
          P (pullback.snd : pullback f g âŸ¶ U)) :
    P.IsLocal := by
  refine âŸ¨hP, ?_, ?_âŸ©
  Â· introv h
    haveI : IsAffine _ := (isAffineOpen_top Y).basicOpen r
    delta morphismRestrict
    rw [affine_cancel_left_isIso hP]
    refine @H _ _ f âŸ¨Scheme.openCoverOfIsIso (ğŸ™ Y), ?_, ?_âŸ© _ (Y.ofRestrict _) _ _
    Â· intro i; dsimp; infer_instance
    Â· intro i; dsimp
      rwa [â† Category.comp_id pullback.snd, â† pullback.condition, affine_cancel_left_isIso hP]
  Â· introv hs hs'
    replace hs := ((isAffineOpen_top Y).basicOpen_union_eq_self_iff _).mpr hs
    have := H f âŸ¨Y.openCoverOfSuprEqTop _ hs, ?_, ?_âŸ© (ğŸ™ _)
    Â· rwa [â† Category.comp_id pullback.snd, â† pullback.condition, affine_cancel_left_isIso hP]
        at this
    Â· intro i; exact (isAffineOpen_top Y).basicOpen _
    Â· rintro (i : s)
      specialize hs' i
      haveI : IsAffine _ := (isAffineOpen_top Y).basicOpen i.1
      delta morphismRestrict at hs'
      rwa [affine_cancel_left_isIso hP] at hs'
#align algebraic_geometry.affine_target_morphism_property.is_local_of_open_cover_imply AlgebraicGeometry.AffineTargetMorphismProperty.isLocalOfOpenCoverImply

theorem targetAffineLocally_iff_of_isAffine [IsAffine Y] :
    targetAffineLocally P f â†” P f := by
  haveI : âˆ€ i, IsAffine (Scheme.OpenCover.obj (Scheme.openCoverOfIsIso (ğŸ™ Y)) i) := fun i => by
    dsimp; infer_instance
  rw [targetAffineLocally_iff_of_openCover hP f (Scheme.openCoverOfIsIso.{0} (ğŸ™ Y))]
  trans P (pullback.snd : pullback f (ğŸ™ _) âŸ¶ _)
  Â· exact âŸ¨fun H => H PUnit.unit, fun H _ => HâŸ©
  rw [â† Category.comp_id pullback.snd, â† pullback.condition, affine_cancel_left_isIso hP.1]
#align algebraic_geometry.affine_target_morphism_property.is_local.affine_target_iff AlgebraicGeometry.targetAffineLocally_iff_of_isAffine

theorem targetAffineLocally_morphsimRestrict_affineOpens
    (U : Y.affineOpens) (H : targetAffineLocally P f) : P (f âˆ£_ U) := by
  rw [â† targetAffineLocally_iff_of_isAffine hP]
  exact targetAffineLocally_morphsimRestrict hP f _ H

theorem targetAffineLocally_pullback_of_isAffine {U} (g : U âŸ¶ Y) [IsOpenImmersion g] [IsAffine U]
    (h : targetAffineLocally P f) : P (pullback.snd : pullback f g âŸ¶ _) := by
  rw [â† targetAffineLocally_iff_of_isAffine hP]
  exact targetAffineLocally_pullback hP f _ h

instance {X : Scheme} (ğ’° : X.AffineOpenCover) (i) : IsAffine (ğ’°.openCover.obj i) := by
  dsimp; infer_instance

def Scheme.OpenCover.pullbackCoverAffineRefinementObj (ğ’° : Y.OpenCover) (i) :
    (ğ’°.affineRefinement.openCover.pullbackCover f).obj i â‰…
      pullback (ğ’°.pullbackHom f i.1) ((ğ’°.obj i.1).affineCover.map i.2) := by
  refine pullbackSymmetry _ _ â‰ªâ‰« (pullbackRightPullbackFstIso _ _ _).symm â‰ªâ‰«
    pullbackSymmetry _ _ â‰ªâ‰«
      asIso (pullback.map _ _ _ _ (pullbackSymmetry _ _).hom (ğŸ™ _) (ğŸ™ _) ?_ ?_)
  Â· simp [pullbackHom]
  Â· simp

@[reassoc (attr := simp)]
lemma Scheme.OpenCover.pullbackCoverAffineRefinementObj_inv_map (ğ’° : Y.OpenCover) (i) :
    (ğ’°.pullbackCoverAffineRefinementObj f i).inv â‰«
      (ğ’°.affineRefinement.openCover.pullbackCover f).map i =
      pullback.fst â‰« (ğ’°.pullbackCover f).map i.1 := by
  simp only [pullbackCover_obj, AffineOpenCover.openCover_obj, AffineOpenCover.openCover_map,
    pullbackCoverAffineRefinementObj, Iso.trans_inv, asIso_inv, Iso.symm_inv, Category.assoc,
    pullbackCover_map, pullbackSymmetry_inv_comp_fst, IsIso.inv_comp_eq, limit.lift_Ï€_assoc, id_eq,
    PullbackCone.mk_pt, cospan_left, PullbackCone.mk_Ï€_app, pullbackSymmetry_hom_comp_fst]
  erw [pullbackRightPullbackFstIso_hom_snd]
  simp only [pullbackSymmetry_inv_comp_snd_assoc]

@[reassoc (attr := simp)]
lemma Scheme.OpenCover.pullbackCoverAffineRefinementObj_inv_pullbackHom (ğ’° : Y.OpenCover) (i) :
    (ğ’°.pullbackCoverAffineRefinementObj f i).inv â‰«
      ğ’°.affineRefinement.openCover.pullbackHom f i =
      pullback.snd := by
  simp only [pullbackCover_obj, pullbackHom, AffineOpenCover.openCover_obj,
    AffineOpenCover.openCover_map, pullbackCoverAffineRefinementObj, Iso.trans_inv, asIso_inv,
    Iso.symm_inv, Category.assoc, pullbackSymmetry_inv_comp_snd, IsIso.inv_comp_eq, limit.lift_Ï€,
    id_eq, PullbackCone.mk_pt, PullbackCone.mk_Ï€_app, Category.comp_id]
  erw [pullbackRightPullbackFstIso_hom_fst]
  simp only [pullbackSymmetry_inv_comp_fst]

theorem targetAffineLocally_isLocal
    {P : AffineTargetMorphismProperty} (hP : P.IsLocal) :
    PropertyIsLocalAtTarget (targetAffineLocally P) := by
  constructor
  Â· exact targetAffineLocally_respectsIso hP.1
  Â· intro X Y f U H V
    rw [â† P.toProperty_apply (i := V.2), hP.1.arrow_mk_iso_iff (morphismRestrictRestrict f _ _)]
    convert H âŸ¨_, V.2.image_of_isOpenImmersion (Y.ofRestrict _)âŸ©
    rw [â† P.toProperty_apply]
  Â· rintro X Y f Us hU H
    let ğ’° := Y.openCoverOfSuprEqTop (fun i : Us â†¦ i) (by rwa [iSup_subtype, â† sSup_eq_iSup])
    apply targetAffineLocally_of_openCover hP _ ğ’°.affineRefinement.openCover
    rintro âŸ¨i, jâŸ©
    have : targetAffineLocally P (ğ’°.pullbackHom f i) := by
      refine ((targetAffineLocally_respectsIso hP.1).arrow_mk_iso_iff
        (morphismRestrictEq _ ?_ â‰ªâ‰« morphismRestrictOpensRange f (ğ’°.map i))).mp (H _ i.2)
      exact (opensRange_Î¹Opens _).symm
    rw [â† affine_cancel_left_isIso hP.1 (ğ’°.pullbackCoverAffineRefinementObj f _).inv,
      ğ’°.pullbackCoverAffineRefinementObj_inv_pullbackHom]
    exact targetAffineLocally_pullback_of_isAffine hP _ ((ğ’°.obj i).affineCover.map j) this
#align algebraic_geometry.affine_target_morphism_property.is_local.target_affine_locally_is_local AlgebraicGeometry.targetAffineLocally_isLocal

end targetAffineLocally

namespace AffineTargetMorphismProperty

/-- A `P : AffineTargetMorphismProperty` is stable under base change if `P` holds for `Y âŸ¶ S`
implies that `P` holds for `X Ã—â‚› Y âŸ¶ X` with `X` and `S` affine schemes. -/
def StableUnderBaseChange (P : AffineTargetMorphismProperty) : Prop :=
  âˆ€ â¦ƒX Y S : Schemeâ¦„ [IsAffine S] [IsAffine X] (f : X âŸ¶ S) (g : Y âŸ¶ S),
    P g â†’ P (pullback.fst : pullback f g âŸ¶ X)
#align algebraic_geometry.affine_target_morphism_property.stable_under_base_change AlgebraicGeometry.AffineTargetMorphismProperty.StableUnderBaseChange

theorem IsLocal.targetAffineLocally_pullback_fst_of_right_of_stableUnderBaseChange
    {P : AffineTargetMorphismProperty} (hP : P.IsLocal) (hP' : P.StableUnderBaseChange)
    {X Y S : Scheme} (f : X âŸ¶ S) (g : Y âŸ¶ S) [IsAffine S] (H : P g) :
    targetAffineLocally P (pullback.fst : pullback f g âŸ¶ X) := by
  rw [targetAffineLocally_iff_of_openCover hP _ X.affineCover]
  intro i
  let e := pullbackSymmetry _ _ â‰ªâ‰« pullbackRightPullbackFstIso f g (X.affineCover.map i)
  have : e.hom â‰« pullback.fst = X.affineCover.pullbackHom pullback.fst i := by
    simp [e, Scheme.OpenCover.pullbackHom]
  rw [â† this, affine_cancel_left_isIso hP.1]
  apply hP'; assumption
#align algebraic_geometry.affine_target_morphism_property.is_local.target_affine_locally_pullback_fst_of_right_of_stable_under_base_change AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal.targetAffineLocally_pullback_fst_of_right_of_stableUnderBaseChange

theorem IsLocal.stableUnderBaseChange {P : AffineTargetMorphismProperty} (hP : P.IsLocal)
    (hP' : P.StableUnderBaseChange) : (targetAffineLocally P).StableUnderBaseChange :=
  MorphismProperty.StableUnderBaseChange.mk (targetAffineLocally_respectsIso hP.respectsIso)
    (fun X Y S f g H => by
      rw [(targetAffineLocally_isLocal hP).iff_of_openCover _ (S.affineCover.pullbackCover f)]
      intro i
      let e : pullback (pullback.fst : pullback f g âŸ¶ _) ((S.affineCover.pullbackCover f).map i) â‰…
          _ := by
        refine pullbackSymmetry _ _ â‰ªâ‰« pullbackRightPullbackFstIso f g _ â‰ªâ‰« ?_ â‰ªâ‰«
          (pullbackRightPullbackFstIso (S.affineCover.map i) g
            (pullback.snd : pullback f (S.affineCover.map i) âŸ¶ _)).symm
        exact asIso
          (pullback.map _ _ _ _ (ğŸ™ _) (ğŸ™ _) (ğŸ™ _) (by simpa using pullback.condition) (by simp))
      have : e.hom â‰« pullback.fst =
          (S.affineCover.pullbackCover f).pullbackHom pullback.fst i := by
        simp [e, Scheme.OpenCover.pullbackHom]
      rw [â† this, (targetAffineLocally_respectsIso hP.1).cancel_left_isIso]
      apply hP.targetAffineLocally_pullback_fst_of_right_of_stableUnderBaseChange hP'
      rw [â† pullbackSymmetry_hom_comp_snd, affine_cancel_left_isIso hP.1]
      apply targetAffineLocally_pullback_of_isAffine hP _ _ H)
#align algebraic_geometry.affine_target_morphism_property.is_local.stable_under_base_change AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal.stableUnderBaseChange

end AffineTargetMorphismProperty

namespace AffineTargetMorphismProperty.IsLocal

@[deprecated (since := "2024-06-22")]
alias targetAffineLocallyIsLocal := targetAffineLocally_isLocal

@[deprecated (since := "2024-06-22")]
alias targetAffineLocallyPullbackFstOfRightOfStableUnderBaseChange :=
  targetAffineLocally_pullback_fst_of_right_of_stableUnderBaseChange

end AffineTargetMorphismProperty.IsLocal

end AlgebraicGeometry
