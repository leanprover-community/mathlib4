/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.AffineScheme
import Mathlib.AlgebraicGeometry.Pullbacks
import Mathlib.CategoryTheory.MorphismProperty.Limits
import Mathlib.Data.List.TFAE

#align_import algebraic_geometry.morphisms.basic from "leanprover-community/mathlib"@"434e2fd21c1900747afc6d13d8be7f4eedba7218"

/-!
# Properties of morphisms between Schemes

We provide the basic framework for talking about properties of morphisms between Schemes.

A `MorphismProperty Scheme` is a predicate on morphisms between schemes. For properties local at
the target, its behaviour is entirely determined by its definition on morphisms into affine schemes,
which we call an `AffineTargetMorphismProperty`. In this file, we provide API lemmas for properties
local at the target, and special support for those properties whose `AffineTargetMorphismProperty`
takes on a more simple form. The main interfaces of the API are the typeclasses
`IsLocalAtTarget` and `HasAffineProperty`, which we describle in detail below.

## `IsLocalAtTarget`

- `AlgebraicGeometry.IsLocalAtTarget`: We say that `IsLocalAtTarget P` for
`P : MorphismProperty Scheme` if
1. `P` respects isomorphisms.
2. If `P` holds for `f : X ‚ü∂ Y`, then `P` holds for `f ‚à£_ U` for any open `U` of `Y`.
3. If `P` holds for `f ‚à£_ U` for an open cover `U` of `Y`, then `P` holds for `f`.

For a morphism property `P` local at the target and `f : X ‚ü∂ Y`, we provide these API lemmas:

- `AlgebraicGeometry.IsLocalAtTarget.of_isPullback`:
    `P` is preserved under pullback along open immersions.
- `AlgebraicGeometry.IsLocalAtTarget.restrict`:
    `P f ‚Üí P (f ‚à£_ U)` for an open `U` of `Y`.
- `AlgebraicGeometry.IsLocalAtTarget.iff_of_iSup_eq_top`:
    `P f ‚Üî ‚àÄ i, P (f ‚à£_ U i)` for a family `U i` of open sets covering `Y`.
- `AlgebraicGeometry.IsLocalAtTarget.iff_of_openCover`:
    `P f ‚Üî ‚àÄ i, P (ùí∞.pullbackHom f i)` for `ùí∞ : Y.openCover`.

## `AffineTargetMorphismProperty`

- `AlgebraicGeometry.AffineTargetMorphismProperty`:
    The type of predicates on `f : X ‚ü∂ Y` with `Y` affine.
- `AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal`: We say that `P.IsLocal` if `P`
    satisfies the assumptions of the affine communication lemma
    (`AlgebraicGeometry.of_affine_open_cover`). That is,
    1. `P` respects isomorphisms.
    2. If `P` holds for `f : X ‚ü∂ Y`, then `P` holds for `f ‚à£_ Y.basicOpen r` for any
      global section `r`.
    3. If `P` holds for `f ‚à£_ Y.basicOpen r` for all `r` in a spanning set of the global sections,
      then `P` holds for `f`.

## `HasAffineProperty`

- `AlgebraicGeometry.HasAffineProperty`:
  `HasAffineProperty P Q` is a type class asserting that `P` is local at the target,
  and over affine schemes, it is equivalent to `Q : AffineTargetMorphismProperty`.

For `HasAffineProperty P Q` and `f : X ‚ü∂ Y`, we provide these API lemmas:

- `AlgebraicGeometry.HasAffineProperty.of_isPullback`:
    `P` is preserved under pullback along open immersions from affine schemes.
- `AlgebraicGeometry.HasAffineProperty.restrict`:
    `P f ‚Üí Q (f ‚à£_ U)` for affine `U` of `Y`.
- `AlgebraicGeometry.HasAffineProperty.iff_of_iSup_eq_top`:
    `P f ‚Üî ‚àÄ i, Q (f ‚à£_ U i)` for a family `U i` of affine open sets covering `Y`.
- `AlgebraicGeometry.HasAffineProperty.iff_of_openCover`:
    `P f ‚Üî ‚àÄ i, P (ùí∞.pullbackHom f i)` for affine open covers `ùí∞` of `Y`.
- `AlgebraicGeometry.HasAffineProperty.stableUnderBaseChange_mk`:
    If `Q` is stable under affine base change, then `P` is stable under arbitrary base change.
-/

set_option linter.uppercaseLean3 false

universe u

open TopologicalSpace CategoryTheory CategoryTheory.Limits Opposite

noncomputable section

namespace AlgebraicGeometry

/--
We say that `P : MorphismProperty Scheme` is local at the target if
1. `P` respects isomorphisms.
2. `P` holds for `f ‚à£_ U` for an open cover `U` of `Y` if and only if `P` holds for `f`.
Also see `IsLocalAtTarget.mk'` for a convenient constructor.
-/
class IsLocalAtTarget (P : MorphismProperty Scheme) : Prop where
  /-- `P` respects isomorphisms. -/
  respectsIso : P.RespectsIso := by infer_instance
  /-- `P` holds for `f ‚à£_ U` for an open cover `U` of `Y` if and only if `P` holds for `f`.  -/
  iff_of_openCover' :
    ‚àÄ {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (ùí∞ : Scheme.OpenCover.{u} Y),
      P f ‚Üî ‚àÄ i, P (ùí∞.pullbackHom f i)
#align algebraic_geometry.property_is_local_at_target AlgebraicGeometry.IsLocalAtTarget

namespace IsLocalAtTarget

attribute [instance] respectsIso

/--
`P` is local at the target if
1. `P` respects isomorphisms.
2. If `P` holds for `f : X ‚ü∂ Y`, then `P` holds for `f ‚à£_ U` for any `U`.
3. If `P` holds for `f ‚à£_ U` for an open cover `U` of `Y`, then `P` holds for `f`.
-/
protected lemma mk' {P : MorphismProperty Scheme} [P.RespectsIso]
    (restrict : ‚àÄ {X Y : Scheme} (f : X ‚ü∂ Y) (U : Opens Y), P f ‚Üí P (f ‚à£_ U))
    (of_sSup_eq_top :
      ‚àÄ {X Y : Scheme.{u}} (f : X ‚ü∂ Y) {Œπ : Type u} (U : Œπ ‚Üí Opens Y), iSup U = ‚ä§ ‚Üí
        (‚àÄ i, P (f ‚à£_ U i)) ‚Üí P f) :
    IsLocalAtTarget P := by
  refine ‚ü®inferInstance, fun {X Y} f ùí∞ ‚Ü¶ ‚ü®?_, fun H ‚Ü¶ of_sSup_eq_top f _ ùí∞.iSup_opensRange ?_‚ü©‚ü©
  ¬∑ exact fun H i ‚Ü¶ (P.arrow_mk_iso_iff (morphismRestrictOpensRange f _)).mp (restrict _ _ H)
  ¬∑ exact fun i ‚Ü¶ (P.arrow_mk_iso_iff (morphismRestrictOpensRange f _)).mpr (H i)

/-- The intersection of two morphism properties that are local at the target is again local at
the target. -/
instance inf (P Q : MorphismProperty Scheme) [IsLocalAtTarget P] [IsLocalAtTarget Q] :
    IsLocalAtTarget (P ‚äì Q) where
  iff_of_openCover' {X Y} f ùí∞ :=
    ‚ü®fun h i ‚Ü¶ ‚ü®(iff_of_openCover' f ùí∞).mp h.left i, (iff_of_openCover' f ùí∞).mp h.right i‚ü©,
     fun h ‚Ü¶ ‚ü®(iff_of_openCover' f ùí∞).mpr (fun i ‚Ü¶ (h i).left),
      (iff_of_openCover' f ùí∞).mpr (fun i ‚Ü¶ (h i).right)‚ü©‚ü©

variable {P} [hP : IsLocalAtTarget P]
variable {X Y U V : Scheme.{u}} {f : X ‚ü∂ Y} {g : U ‚ü∂ Y} [IsOpenImmersion g] (ùí∞ : Y.OpenCover)

lemma of_isPullback {UX UY : Scheme.{u}} {iY : UY ‚ü∂ Y} [IsOpenImmersion iY]
    {iX : UX ‚ü∂ X} {f' : UX ‚ü∂ UY} (h : IsPullback iX f' f iY) (H : P f) : P f' := by
  rw [‚Üê P.cancel_left_of_respectsIso h.isoPullback.inv, h.isoPullback_inv_snd]
  exact (iff_of_openCover' f (Y.affineCover.add iY)).mp H .none

theorem restrict (hf : P f) (U : Opens Y) : P (f ‚à£_ U) :=
  of_isPullback (isPullback_morphismRestrict f U).flip hf

lemma of_iSup_eq_top {Œπ} (U : Œπ ‚Üí Opens Y) (hU : iSup U = ‚ä§)
    (H : ‚àÄ i, P (f ‚à£_ U i)) : P f := by
  refine (IsLocalAtTarget.iff_of_openCover' f
    (Y.openCoverOfSuprEqTop (s := Set.range U) Subtype.val (by ext; simp [‚Üê hU]))).mpr fun i ‚Ü¶ ?_
  obtain ‚ü®_, i, rfl‚ü© := i
  refine (P.arrow_mk_iso_iff (morphismRestrictOpensRange f _)).mp ?_
  show P (f ‚à£_ (Scheme.ŒπOpens (U i)).opensRange)
  rw [opensRange_ŒπOpens]
  exact H i

theorem iff_of_iSup_eq_top {Œπ} (U : Œπ ‚Üí Opens Y) (hU : iSup U = ‚ä§) :
    P f ‚Üî ‚àÄ i, P (f ‚à£_ U i) :=
  ‚ü®fun H _ ‚Ü¶ restrict H _, of_iSup_eq_top U hU‚ü©

lemma of_openCover (H : ‚àÄ i, P (ùí∞.pullbackHom f i)) : P f := by
  apply of_iSup_eq_top (fun i ‚Ü¶ (ùí∞.map i).opensRange) ùí∞.iSup_opensRange
  exact fun i ‚Ü¶ (P.arrow_mk_iso_iff (morphismRestrictOpensRange f _)).mpr (H i)

theorem iff_of_openCover (ùí∞ : Y.OpenCover) :
    P f ‚Üî ‚àÄ i, P (ùí∞.pullbackHom f i) :=
  ‚ü®fun H _ ‚Ü¶ of_isPullback (.of_hasPullback _ _) H, of_openCover _‚ü©

end IsLocalAtTarget

/-- An `AffineTargetMorphismProperty` is a class of morphisms from an arbitrary scheme into an
affine scheme. -/
def AffineTargetMorphismProperty :=
  ‚àÄ ‚¶ÉX Y : Scheme‚¶Ñ (_ : X ‚ü∂ Y) [IsAffine Y], Prop
#align algebraic_geometry.affine_target_morphism_property AlgebraicGeometry.AffineTargetMorphismProperty

namespace AffineTargetMorphismProperty

@[ext]
lemma ext {P Q : AffineTargetMorphismProperty}
    (H : ‚àÄ ‚¶ÉX Y : Scheme‚¶Ñ (f : X ‚ü∂ Y) [IsAffine Y], P f ‚Üî Q f) : P = Q := by
  delta AffineTargetMorphismProperty; ext; exact H _

/-- The restriction of a `MorphismProperty Scheme` to morphisms with affine target. -/
def of (P : MorphismProperty Scheme) : AffineTargetMorphismProperty :=
  fun _ _ f _ ‚Ü¶ P f

/-- An `AffineTargetMorphismProperty` can be extended to a `MorphismProperty` such that it
*never* holds when the target is not affine -/
def toProperty (P : AffineTargetMorphismProperty) :
    MorphismProperty Scheme := fun _ _ f => ‚àÉ h, @P _ _ f h
#align algebraic_geometry.affine_target_morphism_property.to_property AlgebraicGeometry.AffineTargetMorphismProperty.toProperty

theorem toProperty_apply (P : AffineTargetMorphismProperty)
    {X Y : Scheme} (f : X ‚ü∂ Y) [i : IsAffine Y] : P.toProperty f ‚Üî P f := by
  delta AffineTargetMorphismProperty.toProperty; simp [*]
#align algebraic_geometry.affine_target_morphism_property.to_property_apply AlgebraicGeometry.AffineTargetMorphismProperty.toProperty_apply

theorem cancel_left_of_respectsIso
    (P : AffineTargetMorphismProperty) [P.toProperty.RespectsIso]
    {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsIso f] [IsAffine Z] : P (f ‚â´ g) ‚Üî P g := by
  rw [‚Üê P.toProperty_apply, ‚Üê P.toProperty_apply, P.toProperty.cancel_left_of_respectsIso]
#align algebraic_geometry.affine_cancel_left_is_iso AlgebraicGeometry.AffineTargetMorphismProperty.cancel_left_of_respectsIso

theorem cancel_right_of_respectsIso
    (P : AffineTargetMorphismProperty) [P.toProperty.RespectsIso]
    {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsIso g] [IsAffine Z] [IsAffine Y] :
    P (f ‚â´ g) ‚Üî P f := by rw [‚Üê P.toProperty_apply, ‚Üê P.toProperty_apply,
      P.toProperty.cancel_right_of_respectsIso]
#align algebraic_geometry.affine_cancel_right_is_iso AlgebraicGeometry.AffineTargetMorphismProperty.cancel_right_of_respectsIso

@[deprecated (since := "2024-07-02")] alias affine_cancel_left_isIso :=
  AffineTargetMorphismProperty.cancel_left_of_respectsIso
@[deprecated (since := "2024-07-02")] alias affine_cancel_right_isIso :=
  AffineTargetMorphismProperty.cancel_right_of_respectsIso

theorem arrow_mk_iso_iff
    (P : AffineTargetMorphismProperty) [P.toProperty.RespectsIso]
    {X Y X' Y' : Scheme} {f : X ‚ü∂ Y} {f' : X' ‚ü∂ Y'}
    (e : Arrow.mk f ‚âÖ Arrow.mk f') {h : IsAffine Y} :
    letI : IsAffine Y' := isAffine_of_isIso (Y := Y) e.inv.right
    P f ‚Üî P f' := by
  rw [‚Üê P.toProperty_apply, ‚Üê P.toProperty_apply, P.toProperty.arrow_mk_iso_iff e]

theorem respectsIso_mk {P : AffineTargetMorphismProperty}
    (h‚ÇÅ : ‚àÄ {X Y Z} (e : X ‚âÖ Y) (f : Y ‚ü∂ Z) [IsAffine Z], P f ‚Üí P (e.hom ‚â´ f))
    (h‚ÇÇ : ‚àÄ {X Y Z} (e : Y ‚âÖ Z) (f : X ‚ü∂ Y) [h : IsAffine Y],
      P f ‚Üí @P _ _ (f ‚â´ e.hom) (isAffine_of_isIso e.inv)) :
    P.toProperty.RespectsIso := by
  constructor
  ¬∑ rintro X Y Z e f ‚ü®a, h‚ü©; exact ‚ü®a, h‚ÇÅ e f h‚ü©
  ¬∑ rintro X Y Z e f ‚ü®a, h‚ü©; exact ‚ü®isAffine_of_isIso e.inv, h‚ÇÇ e f h‚ü©
#align algebraic_geometry.affine_target_morphism_property.respects_iso_mk AlgebraicGeometry.AffineTargetMorphismProperty.respectsIso_mk

instance respectsIso_of
    (P : MorphismProperty Scheme) [P.RespectsIso] :
    (of P).toProperty.RespectsIso := by
  apply respectsIso_mk
  ¬∑ intro _ _ _ _ _ _; apply MorphismProperty.RespectsIso.precomp
  ¬∑ intro _ _ _ _ _ _; apply MorphismProperty.RespectsIso.postcomp

/-- We say that `P : AffineTargetMorphismProperty` is a local property if
1. `P` respects isomorphisms.
2. If `P` holds for `f : X ‚ü∂ Y`, then `P` holds for `f ‚à£_ Y.basicOpen r` for any
  global section `r`.
3. If `P` holds for `f ‚à£_ Y.basicOpen r` for all `r` in a spanning set of the global sections,
  then `P` holds for `f`.
-/
class IsLocal (P : AffineTargetMorphismProperty) : Prop where
  /-- `P` as a morphism property respects isomorphisms -/
  respectsIso : P.toProperty.RespectsIso
  /-- `P` is stable under restriction to basic open set of global sections. -/
  to_basicOpen :
    ‚àÄ {X Y : Scheme} [IsAffine Y] (f : X ‚ü∂ Y) (r : Œì(Y, ‚ä§)), P f ‚Üí P (f ‚à£_ Y.basicOpen r)
  /-- `P` for `f` if `P` holds for `f` restricted to basic sets of a spanning set of the global
    sections -/
  of_basicOpenCover :
    ‚àÄ {X Y : Scheme} [IsAffine Y] (f : X ‚ü∂ Y) (s : Finset Œì(Y, ‚ä§))
      (_ : Ideal.span (s : Set Œì(Y, ‚ä§)) = ‚ä§), (‚àÄ r : s, P (f ‚à£_ Y.basicOpen r.1)) ‚Üí P f
#align algebraic_geometry.affine_target_morphism_property.is_local AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal

attribute [instance] AffineTargetMorphismProperty.IsLocal.respectsIso

open AffineTargetMorphismProperty in
instance (P : MorphismProperty Scheme) [IsLocalAtTarget P] : (of P).IsLocal where
  respectsIso := inferInstance
  to_basicOpen _ _ H := IsLocalAtTarget.restrict H _
  of_basicOpenCover {_ Y} _ _ _ hs := IsLocalAtTarget.of_iSup_eq_top _
    (((isAffineOpen_top Y).basicOpen_union_eq_self_iff _).mpr hs)

/-- A `P : AffineTargetMorphismProperty` is stable under base change if `P` holds for `Y ‚ü∂ S`
implies that `P` holds for `X √ó‚Çõ Y ‚ü∂ X` with `X` and `S` affine schemes. -/
def StableUnderBaseChange (P : AffineTargetMorphismProperty) : Prop :=
  ‚àÄ ‚¶ÉZ X Y S : Scheme‚¶Ñ [IsAffine S] [IsAffine X] {f : X ‚ü∂ S} {g : Y ‚ü∂ S}
    {f' : Z ‚ü∂ Y} {g' : Z ‚ü∂ X}, IsPullback g' f' f g ‚Üí P g ‚Üí P g'
#align algebraic_geometry.affine_target_morphism_property.stable_under_base_change AlgebraicGeometry.AffineTargetMorphismProperty.StableUnderBaseChange

lemma StableUnderBaseChange.mk (P : AffineTargetMorphismProperty) [P.toProperty.RespectsIso]
    (H : ‚àÄ ‚¶ÉX Y S : Scheme‚¶Ñ [IsAffine S] [IsAffine X] (f : X ‚ü∂ S) (g : Y ‚ü∂ S),
      P g ‚Üí P (pullback.fst f g)) : P.StableUnderBaseChange := by
  intros Z X Y S _ _ f g f' g' h hg
  rw [‚Üê P.cancel_left_of_respectsIso h.isoPullback.inv, h.isoPullback_inv_fst]
  exact H f g hg

end AffineTargetMorphismProperty

section targetAffineLocally

/-- For a `P : AffineTargetMorphismProperty`, `targetAffineLocally P` holds for
`f : X ‚ü∂ Y` whenever `P` holds for the restriction of `f` on every affine open subset of `Y`. -/
def targetAffineLocally (P : AffineTargetMorphismProperty) : MorphismProperty Scheme :=
  fun {X Y : Scheme} (f : X ‚ü∂ Y) => ‚àÄ U : Y.affineOpens, P (f ‚à£_ U)
#align algebraic_geometry.target_affine_locally AlgebraicGeometry.targetAffineLocally

theorem of_targetAffineLocally_of_isPullback
    {P : AffineTargetMorphismProperty} [P.IsLocal]
    {X Y UX UY : Scheme.{u}} [IsAffine UY] {f : X ‚ü∂ Y} {iY : UY ‚ü∂ Y} [IsOpenImmersion iY]
    {iX : UX ‚ü∂ X} {f' : UX ‚ü∂ UY} (h : IsPullback iX f' f iY) (hf : targetAffineLocally P f) :
    P f' := by
  rw [‚Üê P.cancel_left_of_respectsIso h.isoPullback.inv, h.isoPullback_inv_snd]
  exact (P.arrow_mk_iso_iff
    (morphismRestrictOpensRange f _)).mp (hf ‚ü®_, isAffineOpen_opensRange iY‚ü©)

instance (P : AffineTargetMorphismProperty) [P.toProperty.RespectsIso] :
    (targetAffineLocally P).RespectsIso := by
  constructor
  ¬∑ introv H U
    rw [morphismRestrict_comp, P.cancel_left_of_respectsIso]
    exact H U
  ¬∑ introv H
    rintro ‚ü®U, hU : IsAffineOpen U‚ü©; dsimp
    haveI : IsAffine _ := hU.preimage_of_isIso e.hom
    rw [morphismRestrict_comp, P.cancel_right_of_respectsIso]
    exact H ‚ü®(Opens.map e.hom.val.base).obj U, hU.preimage_of_isIso e.hom‚ü©

/--
`HasAffineProperty P Q` is a type class asserting that `P` is local at the target, and over affine
schemes, it is equivalent to `Q : AffineTargetMorphismProperty`.
To make the proofs easier, we state it instead as
1. `Q` is local at the target
2. `P f` if and only if `‚àÄ U, Q (f ‚à£_ U)` ranging over all affine opens of `U`.
See `HasAffineProperty.iff`.
-/
class HasAffineProperty (P : MorphismProperty Scheme)
    (Q : outParam AffineTargetMorphismProperty) : Prop where
  isLocal_affineProperty : Q.IsLocal
  eq_targetAffineLocally' : P = targetAffineLocally Q

namespace HasAffineProperty

variable (P : MorphismProperty Scheme) {Q} [HasAffineProperty P Q]
variable {X Y : Scheme.{u}} {f : X ‚ü∂ Y}

instance (Q : AffineTargetMorphismProperty) [Q.IsLocal] :
    HasAffineProperty (targetAffineLocally Q) Q :=
  ‚ü®inferInstance, rfl‚ü©

lemma eq_targetAffineLocally : P = targetAffineLocally Q := eq_targetAffineLocally'

/-- Every property local at the target can be associated with an affine target property.
This is not an instance as the associated property can often take on simpler forms. -/
lemma of_isLocalAtTarget (P) [IsLocalAtTarget P] :
    HasAffineProperty P (AffineTargetMorphismProperty.of P) where
  isLocal_affineProperty := inferInstance
  eq_targetAffineLocally' := by
    ext X Y f
    constructor
    ¬∑ intro hf ‚ü®U, hU‚ü©
      exact IsLocalAtTarget.restrict hf _
    ¬∑ intro hf
      exact IsLocalAtTarget.of_openCover (P := P) Y.affineCover
        fun i ‚Ü¶ of_targetAffineLocally_of_isPullback (.of_hasPullback _ _) hf

lemma copy {P P'} {Q Q'} [HasAffineProperty P Q]
    (e : P = P') (e' : Q = Q') : HasAffineProperty P' Q' where
  isLocal_affineProperty := e' ‚ñ∏ isLocal_affineProperty P
  eq_targetAffineLocally' := e' ‚ñ∏ e.symm ‚ñ∏ eq_targetAffineLocally P

variable {P}

theorem of_isPullback {UX UY : Scheme.{u}} [IsAffine UY] {iY : UY ‚ü∂ Y} [IsOpenImmersion iY]
    {iX : UX ‚ü∂ X} {f' : UX ‚ü∂ UY} (h : IsPullback iX f' f iY) (hf : P f) :
    Q f' :=
  letI := isLocal_affineProperty P
  of_targetAffineLocally_of_isPullback h (eq_targetAffineLocally (P := P) ‚ñ∏ hf)

theorem restrict (hf : P f) (U : Y.affineOpens) :
    Q (f ‚à£_ U) :=
  of_isPullback (isPullback_morphismRestrict f U).flip hf

instance (priority := 900) : P.RespectsIso := by
  letI := isLocal_affineProperty P
  rw [eq_targetAffineLocally P]
  infer_instance
#align algebraic_geometry.target_affine_locally_respects_iso AlgebraicGeometry.HasAffineProperty.instRespectsIsoScheme

theorem of_iSup_eq_top
    {Œπ} (U : Œπ ‚Üí Y.affineOpens) (hU : ‚®Ü i, (U i : Opens Y) = ‚ä§)
    (hU' : ‚àÄ i, Q (f ‚à£_ U i)) :
    P f := by
  letI := isLocal_affineProperty P
  rw [eq_targetAffineLocally P]
  classical
  intro V
  induction V using of_affine_open_cover U hU  with
  | basicOpen U r h =>
    haveI : IsAffine _ := U.2
    have := AffineTargetMorphismProperty.IsLocal.to_basicOpen (f ‚à£_ U.1) ((Y.resTop U).inv r) h
    exact (Q.arrow_mk_iso_iff
      (morphismRestrictRestrictBasicOpen f _ r)).mp this
  | openCover U s hs H =>
    apply AffineTargetMorphismProperty.IsLocal.of_basicOpenCover _
      (s.image (Y.resTop _).inv) (by simp [‚Üê Ideal.map_span, hs, Ideal.map_top])
    intro ‚ü®r, hr‚ü©
    obtain ‚ü®r, hr', rfl‚ü© := Finset.mem_image.mp hr
    exact (Q.arrow_mk_iso_iff
      (morphismRestrictRestrictBasicOpen f _ r).symm).mp (H ‚ü®r, hr'‚ü©)
  | hU i => exact hU' i

theorem iff_of_iSup_eq_top
    {Œπ} (U : Œπ ‚Üí Y.affineOpens) (hU : ‚®Ü i, (U i : Opens Y) = ‚ä§) :
    P f ‚Üî ‚àÄ i, Q (f ‚à£_ U i) :=
  ‚ü®fun H _ ‚Ü¶ restrict H _, fun H ‚Ü¶ HasAffineProperty.of_iSup_eq_top U hU H‚ü©

theorem of_openCover
    (ùí∞ : Y.OpenCover) [‚àÄ i, IsAffine (ùí∞.obj i)] (hùí∞ : ‚àÄ i, Q (ùí∞.pullbackHom f i)) :
    P f :=
  letI := isLocal_affineProperty P
  of_iSup_eq_top
    (fun i ‚Ü¶ ‚ü®_, isAffineOpen_opensRange (ùí∞.map i)‚ü©) ùí∞.iSup_opensRange
    (fun i ‚Ü¶ (Q.arrow_mk_iso_iff (morphismRestrictOpensRange f _)).mpr (hùí∞ i))
#align algebraic_geometry.target_affine_locally_of_open_cover AlgebraicGeometry.HasAffineProperty.of_openCover

theorem iff_of_openCover (ùí∞ : Y.OpenCover) [‚àÄ i, IsAffine (ùí∞.obj i)] :
    P f ‚Üî ‚àÄ i, Q (ùí∞.pullbackHom f i) := by
  letI := isLocal_affineProperty P
  rw [iff_of_iSup_eq_top (P := P)
    (fun i ‚Ü¶ ‚ü®_, isAffineOpen_opensRange _‚ü©) ùí∞.iSup_opensRange]
  exact forall_congr' fun i ‚Ü¶ Q.arrow_mk_iso_iff
    (morphismRestrictOpensRange f _)

theorem iff_of_isAffine [IsAffine Y] : P f ‚Üî Q f := by
  letI := isLocal_affineProperty P
  haveI : ‚àÄ i, IsAffine (Scheme.OpenCover.obj (Scheme.openCoverOfIsIso (ùüô Y)) i) := fun i => by
    dsimp; infer_instance
  rw [iff_of_openCover (P := P) (Scheme.openCoverOfIsIso.{0} (ùüô Y))]
  trans Q (pullback.snd f (ùüô _))
  ¬∑ exact ‚ü®fun H => H PUnit.unit, fun H _ => H‚ü©
  rw [‚Üê Category.comp_id (pullback.snd _ _), ‚Üê pullback.condition,
    Q.cancel_left_of_respectsIso]
#align algebraic_geometry.affine_target_morphism_property.is_local.affine_target_iff AlgebraicGeometry.HasAffineProperty.iff_of_isAffine

instance (priority := 900) : IsLocalAtTarget P := by
  letI := isLocal_affineProperty P
  apply IsLocalAtTarget.mk'
  ¬∑ rw [eq_targetAffineLocally P]
    intro X Y f U H V
    rw [Q.arrow_mk_iso_iff (morphismRestrictRestrict f _ _)]
    exact H ‚ü®_, V.2.image_of_isOpenImmersion (Y.ofRestrict _)‚ü©
  ¬∑ rintro X Y f Œπ U hU H
    let ùí∞ := Y.openCoverOfSuprEqTop U hU
    apply of_openCover ùí∞.affineRefinement.openCover
    rintro ‚ü®i, j‚ü©
    have : P (ùí∞.pullbackHom f i) := by
      refine (P.arrow_mk_iso_iff
        (morphismRestrictEq _ ?_ ‚â™‚â´ morphismRestrictOpensRange f (ùí∞.map i))).mp (H i)
      exact (opensRange_ŒπOpens _).symm
    rw [‚Üê Q.cancel_left_of_respectsIso (ùí∞.pullbackCoverAffineRefinementObjIso f _).inv,
      ùí∞.pullbackCoverAffineRefinementObjIso_inv_pullbackHom]
    exact of_isPullback (.of_hasPullback _ _) this
#align algebraic_geometry.affine_target_morphism_property.is_local.target_affine_locally_is_local AlgebraicGeometry.HasAffineProperty.instIsLocalAtTarget

open AffineTargetMorphismProperty in
protected theorem iff :
    HasAffineProperty P Q ‚Üî IsLocalAtTarget P ‚àß Q = of P :=
  ‚ü®fun _ ‚Ü¶ ‚ü®inferInstance, ext fun _ _ _ ‚Ü¶ iff_of_isAffine.symm‚ü©,
    fun ‚ü®_, e‚ü© ‚Ü¶ e ‚ñ∏ of_isLocalAtTarget P‚ü©

private theorem pullback_fst_of_right (hP' : Q.StableUnderBaseChange)
    {X Y S : Scheme} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [IsAffine S] (H : Q g) :
    P (pullback.fst f g) := by
  letI := isLocal_affineProperty P
  rw [iff_of_openCover (P := P) X.affineCover]
  intro i
  let e := pullbackSymmetry _ _ ‚â™‚â´ pullbackRightPullbackFstIso f g (X.affineCover.map i)
  have : e.hom ‚â´ pullback.fst _ _ = X.affineCover.pullbackHom (pullback.fst _ _) i := by
    simp [e, Scheme.OpenCover.pullbackHom]
  rw [‚Üê this, Q.cancel_left_of_respectsIso]
  apply hP' (.of_hasPullback _ _)
  exact H

theorem stableUnderBaseChange (hP' : Q.StableUnderBaseChange) :
    P.StableUnderBaseChange :=
  MorphismProperty.StableUnderBaseChange.mk
    (fun X Y S f g H => by
      rw [IsLocalAtTarget.iff_of_openCover (P := P) (S.affineCover.pullbackCover f)]
      intro i
      let e : pullback (pullback.fst f g) ((S.affineCover.pullbackCover f).map i) ‚âÖ
          _ := by
        refine pullbackSymmetry _ _ ‚â™‚â´ pullbackRightPullbackFstIso f g _ ‚â™‚â´ ?_ ‚â™‚â´
          (pullbackRightPullbackFstIso (S.affineCover.map i) g
            (pullback.snd f (S.affineCover.map i))).symm
        exact asIso
          (pullback.map _ _ _ _ (ùüô _) (ùüô _) (ùüô _) (by simpa using pullback.condition) (by simp))
      have : e.hom ‚â´ pullback.fst _ _ =
          (S.affineCover.pullbackCover f).pullbackHom (pullback.fst _ _) i := by
        simp [e, Scheme.OpenCover.pullbackHom]
      rw [‚Üê this, P.cancel_left_of_respectsIso]
      apply HasAffineProperty.pullback_fst_of_right hP'
      letI := isLocal_affineProperty P
      rw [‚Üê pullbackSymmetry_hom_comp_snd, Q.cancel_left_of_respectsIso]
      apply of_isPullback (.of_hasPullback _ _) H)
#align algebraic_geometry.affine_target_morphism_property.is_local.stable_under_base_change AlgebraicGeometry.HasAffineProperty.stableUnderBaseChange

end HasAffineProperty

end targetAffineLocally

end AlgebraicGeometry
