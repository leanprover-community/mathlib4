/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.AffineScheme
import Mathlib.AlgebraicGeometry.Pullbacks
import Mathlib.CategoryTheory.MorphismProperty.Limits
import Mathlib.Data.List.TFAE

/-!
# Properties of morphisms between Schemes

We provide the basic framework for talking about properties of morphisms between Schemes.

A `MorphismProperty Scheme` is a predicate on morphisms between schemes. For properties local at
the target, its behaviour is entirely determined by its definition on morphisms into affine schemes,
which we call an `AffineTargetMorphismProperty`. In this file, we provide API lemmas for properties
local at the target, and special support for those properties whose `AffineTargetMorphismProperty`
takes on a more simple form. We also provide API lemmas for properties local at the target.
The main interfaces of the API are the typeclasses `IsLocalAtTarget`, `IsLocalAtSource` and
`HasAffineProperty`, which we describle in detail below.

## `IsLocalAtTarget`

- `AlgebraicGeometry.IsLocalAtTarget`: We say that `IsLocalAtTarget P` for
`P : MorphismProperty Scheme` if
1. `P` respects isomorphisms.
2. `P` holds for `f âˆ£_ U` for an open cover `U` of `Y` if and only if `P` holds for `f`.

For a morphism property `P` local at the target and `f : X âŸ¶ Y`, we provide these API lemmas:

- `AlgebraicGeometry.IsLocalAtTarget.of_isPullback`:
    `P` is preserved under pullback along open immersions.
- `AlgebraicGeometry.IsLocalAtTarget.restrict`:
    `P f â†’ P (f âˆ£_ U)` for an open `U` of `Y`.
- `AlgebraicGeometry.IsLocalAtTarget.iff_of_iSup_eq_top`:
    `P f â†” âˆ€ i, P (f âˆ£_ U i)` for a family `U i` of open sets covering `Y`.
- `AlgebraicGeometry.IsLocalAtTarget.iff_of_openCover`:
    `P f â†” âˆ€ i, P (ğ’°.pullbackHom f i)` for `ğ’° : Y.openCover`.

## `IsLocalAtSource`

- `AlgebraicGeometry.IsLocalAtSource`: We say that `IsLocalAtSource P` for
`P : MorphismProperty Scheme` if
1. `P` respects isomorphisms.
2. `P` holds for `ğ’°.map i â‰« f` for an open cover `ğ’°` of `X` iff `P` holds for `f : X âŸ¶ Y`.

For a morphism property `P` local at the source and `f : X âŸ¶ Y`, we provide these API lemmas:

- `AlgebraicGeometry.IsLocalAtTarget.comp`:
    `P` is preserved under composition with open immersions at the source.
- `AlgebraicGeometry.IsLocalAtTarget.iff_of_iSup_eq_top`:
    `P f â†” âˆ€ i, P (U.Î¹ â‰« f)` for a family `U i` of open sets covering `X`.
- `AlgebraicGeometry.IsLocalAtTarget.iff_of_openCover`:
    `P f â†” âˆ€ i, P (ğ’°.map i â‰« f)` for `ğ’° : X.openCover`.
- `AlgebraicGeometry.IsLocalAtTarget.of_isOpenImmersion`: If `P` contains identities then `P` holds
    for open immersions.

## `AffineTargetMorphismProperty`

- `AlgebraicGeometry.AffineTargetMorphismProperty`:
    The type of predicates on `f : X âŸ¶ Y` with `Y` affine.
- `AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal`: We say that `P.IsLocal` if `P`
    satisfies the assumptions of the affine communication lemma
    (`AlgebraicGeometry.of_affine_open_cover`). That is,
    1. `P` respects isomorphisms.
    2. If `P` holds for `f : X âŸ¶ Y`, then `P` holds for `f âˆ£_ Y.basicOpen r` for any
      global section `r`.
    3. If `P` holds for `f âˆ£_ Y.basicOpen r` for all `r` in a spanning set of the global sections,
      then `P` holds for `f`.

## `HasAffineProperty`

- `AlgebraicGeometry.HasAffineProperty`:
  `HasAffineProperty P Q` is a type class asserting that `P` is local at the target,
  and over affine schemes, it is equivalent to `Q : AffineTargetMorphismProperty`.

For `HasAffineProperty P Q` and `f : X âŸ¶ Y`, we provide these API lemmas:

- `AlgebraicGeometry.HasAffineProperty.of_isPullback`:
    `P` is preserved under pullback along open immersions from affine schemes.
- `AlgebraicGeometry.HasAffineProperty.restrict`:
    `P f â†’ Q (f âˆ£_ U)` for affine `U` of `Y`.
- `AlgebraicGeometry.HasAffineProperty.iff_of_iSup_eq_top`:
    `P f â†” âˆ€ i, Q (f âˆ£_ U i)` for a family `U i` of affine open sets covering `Y`.
- `AlgebraicGeometry.HasAffineProperty.iff_of_openCover`:
    `P f â†” âˆ€ i, P (ğ’°.pullbackHom f i)` for affine open covers `ğ’°` of `Y`.
- `AlgebraicGeometry.HasAffineProperty.isStableUnderBaseChange_mk`:
    If `Q` is stable under affine base change, then `P` is stable under arbitrary base change.
-/


universe u v

open TopologicalSpace CategoryTheory CategoryTheory.Limits Opposite

noncomputable section

namespace AlgebraicGeometry

/--
We say that `P : MorphismProperty Scheme` is local at the target if
1. `P` respects isomorphisms.
2. `P` holds for `f âˆ£_ U` for an open cover `U` of `Y` if and only if `P` holds for `f`.
Also see `IsLocalAtTarget.mk'` for a convenient constructor.
-/
class IsLocalAtTarget (P : MorphismProperty Scheme) : Prop where
  /-- `P` respects isomorphisms. -/
  respectsIso : P.RespectsIso := by infer_instance
  /-- `P` holds for `f âˆ£_ U` for an open cover `U` of `Y` if and only if `P` holds for `f`. -/
  iff_of_openCover' :
    âˆ€ {X Y : Scheme.{u}} (f : X âŸ¶ Y) (ğ’° : Scheme.OpenCover.{u} Y),
      P f â†” âˆ€ i, P (ğ’°.pullbackHom f i)

namespace IsLocalAtTarget

attribute [instance] respectsIso

/--
`P` is local at the target if
1. `P` respects isomorphisms.
2. If `P` holds for `f : X âŸ¶ Y`, then `P` holds for `f âˆ£_ U` for any `U`.
3. If `P` holds for `f âˆ£_ U` for an open cover `U` of `Y`, then `P` holds for `f`.
-/
protected lemma mk' {P : MorphismProperty Scheme} [P.RespectsIso]
    (restrict : âˆ€ {X Y : Scheme} (f : X âŸ¶ Y) (U : Y.Opens), P f â†’ P (f âˆ£_ U))
    (of_sSup_eq_top :
      âˆ€ {X Y : Scheme.{u}} (f : X âŸ¶ Y) {Î¹ : Type u} (U : Î¹ â†’ Y.Opens), iSup U = âŠ¤ â†’
        (âˆ€ i, P (f âˆ£_ U i)) â†’ P f) :
    IsLocalAtTarget P := by
  refine âŸ¨inferInstance, fun {X Y} f ğ’° â†¦ âŸ¨?_, fun H â†¦ of_sSup_eq_top f _ ğ’°.iSup_opensRange ?_âŸ©âŸ©
  Â· exact fun H i â†¦ (P.arrow_mk_iso_iff (morphismRestrictOpensRange f _)).mp (restrict _ _ H)
  Â· exact fun i â†¦ (P.arrow_mk_iso_iff (morphismRestrictOpensRange f _)).mpr (H i)

/-- The intersection of two morphism properties that are local at the target is again local at
the target. -/
instance inf (P Q : MorphismProperty Scheme) [IsLocalAtTarget P] [IsLocalAtTarget Q] :
    IsLocalAtTarget (P âŠ“ Q) where
  iff_of_openCover' {_ _} f ğ’° :=
    âŸ¨fun h i â†¦ âŸ¨(iff_of_openCover' f ğ’°).mp h.left i, (iff_of_openCover' f ğ’°).mp h.right iâŸ©,
     fun h â†¦ âŸ¨(iff_of_openCover' f ğ’°).mpr (fun i â†¦ (h i).left),
      (iff_of_openCover' f ğ’°).mpr (fun i â†¦ (h i).right)âŸ©âŸ©

variable {P} [hP : IsLocalAtTarget P] {X Y : Scheme.{u}} {f : X âŸ¶ Y} (ğ’° : Y.OpenCover)

lemma of_isPullback {UX UY : Scheme.{u}} {iY : UY âŸ¶ Y} [IsOpenImmersion iY]
    {iX : UX âŸ¶ X} {f' : UX âŸ¶ UY} (h : IsPullback iX f' f iY) (H : P f) : P f' := by
  rw [â† P.cancel_left_of_respectsIso h.isoPullback.inv, h.isoPullback_inv_snd]
  exact (iff_of_openCover' f (Y.affineCover.add iY)).mp H .none

theorem restrict (hf : P f) (U : Y.Opens) : P (f âˆ£_ U) :=
  of_isPullback (isPullback_morphismRestrict f U).flip hf

lemma of_iSup_eq_top {Î¹} (U : Î¹ â†’ Y.Opens) (hU : iSup U = âŠ¤)
    (H : âˆ€ i, P (f âˆ£_ U i)) : P f := by
  refine (IsLocalAtTarget.iff_of_openCover' f
    (Y.openCoverOfISupEqTop (s := Set.range U) Subtype.val (by ext; simp [â† hU]))).mpr fun i â†¦ ?_
  obtain âŸ¨_, i, rflâŸ© := i
  refine (P.arrow_mk_iso_iff (morphismRestrictOpensRange f _)).mp ?_
  show P (f âˆ£_ (U i).Î¹.opensRange)
  rw [Scheme.Opens.opensRange_Î¹]
  exact H i

theorem iff_of_iSup_eq_top {Î¹} (U : Î¹ â†’ Y.Opens) (hU : iSup U = âŠ¤) :
    P f â†” âˆ€ i, P (f âˆ£_ U i) :=
  âŸ¨fun H _ â†¦ restrict H _, of_iSup_eq_top U hUâŸ©

lemma of_openCover (H : âˆ€ i, P (ğ’°.pullbackHom f i)) : P f := by
  apply of_iSup_eq_top (fun i â†¦ (ğ’°.map i).opensRange) ğ’°.iSup_opensRange
  exact fun i â†¦ (P.arrow_mk_iso_iff (morphismRestrictOpensRange f _)).mpr (H i)

theorem iff_of_openCover (ğ’° : Y.OpenCover) :
    P f â†” âˆ€ i, P (ğ’°.pullbackHom f i) :=
  âŸ¨fun H _ â†¦ of_isPullback (.of_hasPullback _ _) H, of_openCover _âŸ©

lemma of_range_subset_iSup [P.RespectsRight @IsOpenImmersion] {Î¹ : Type*} (U : Î¹ â†’ Y.Opens)
    (H : Set.range f.base âŠ† (â¨† i, U i : Y.Opens)) (hf : âˆ€ i, P (f âˆ£_ U i)) : P f := by
  let g : X âŸ¶ (â¨† i, U i : Y.Opens) := IsOpenImmersion.lift (Scheme.Opens.Î¹ _) f (by simpa using H)
  rw [â† IsOpenImmersion.lift_fac (â¨† i, U i).Î¹ f (by simpa using H)]
  apply MorphismProperty.RespectsRight.postcomp (Q := @IsOpenImmersion) _ inferInstance
  rw [IsLocalAtTarget.iff_of_iSup_eq_top (P := P) (U := fun i : Î¹ â†¦ (â¨† i, U i).Î¹ â»Â¹áµ U i)]
  Â· intro i
    have heq : g â»Â¹áµ (â¨† i, U i).Î¹ â»Â¹áµ U i = f â»Â¹áµ U i := by
      show (g â‰« (â¨† i, U i).Î¹) â»Â¹áµ U i = _
      simp [g]
    let e : Arrow.mk (g âˆ£_ (â¨† i, U i).Î¹ â»Â¹áµ U i) â‰… Arrow.mk (f âˆ£_ U i) :=
        Arrow.isoMk (X.isoOfEq heq) (Scheme.Opens.isoOfLE (le_iSup U i)) <| by
      simp [â† CategoryTheory.cancel_mono (U i).Î¹, g]
    rw [P.arrow_mk_iso_iff e]
    exact hf i
  apply (â¨† i, U i).Î¹.image_injective
  dsimp
  rw [Scheme.Hom.image_iSup, Scheme.Hom.image_top_eq_opensRange, Scheme.Opens.opensRange_Î¹]
  simp [Scheme.Hom.image_preimage_eq_opensRange_inter, le_iSup U]

instance top : IsLocalAtTarget (âŠ¤ : MorphismProperty Scheme.{u}) where
  iff_of_openCover' := by simp

end IsLocalAtTarget

/--
We say that `P : MorphismProperty Scheme` is local at the source if
1. `P` respects isomorphisms.
2. `P` holds for `ğ’°.map i â‰« f` for an open cover `ğ’°` of `X` iff `P` holds for `f : X âŸ¶ Y`.
Also see `IsLocalAtSource.mk'` for a convenient constructor.
-/
class IsLocalAtSource (P : MorphismProperty Scheme) : Prop where
  /-- `P` respects isomorphisms. -/
  respectsIso : P.RespectsIso := by infer_instance
  /-- `P` holds for `f âˆ£_ U` for an open cover `U` of `Y` if and only if `P` holds for `f`. -/
  iff_of_openCover' :
    âˆ€ {X Y : Scheme.{u}} (f : X âŸ¶ Y) (ğ’° : Scheme.OpenCover.{u} X),
      P f â†” âˆ€ i, P (ğ’°.map i â‰« f)

namespace IsLocalAtSource

attribute [instance] respectsIso

/--
`P` is local at the source if
1. `P` respects isomorphisms.
2. If `P` holds for `f : X âŸ¶ Y`, then `P` holds for `U.Î¹ â‰« f` for any `U`.
3. If `P` holds for `U.Î¹ â‰« f` for an open cover `U` of `X`, then `P` holds for `f`.
-/
protected lemma mk' {P : MorphismProperty Scheme} [P.RespectsIso]
    (restrict : âˆ€ {X Y : Scheme} (f : X âŸ¶ Y) (U : X.Opens), P f â†’ P (U.Î¹ â‰« f))
    (of_sSup_eq_top :
      âˆ€ {X Y : Scheme.{u}} (f : X âŸ¶ Y) {Î¹ : Type u} (U : Î¹ â†’ X.Opens), iSup U = âŠ¤ â†’
        (âˆ€ i, P ((U i).Î¹ â‰« f)) â†’ P f) :
    IsLocalAtSource P := by
  refine âŸ¨inferInstance, fun {X Y} f ğ’° â†¦
    âŸ¨fun H i â†¦ ?_, fun H â†¦ of_sSup_eq_top f _ ğ’°.iSup_opensRange fun i â†¦ ?_âŸ©âŸ©
  Â· rw [â† IsOpenImmersion.isoOfRangeEq_hom_fac (ğ’°.map i) (Scheme.Opens.Î¹ _)
      (congr_arg Opens.carrier (ğ’°.map i).opensRange.opensRange_Î¹.symm), Category.assoc,
      P.cancel_left_of_respectsIso]
    exact restrict _ _ H
  Â· rw [â† IsOpenImmersion.isoOfRangeEq_inv_fac (ğ’°.map i) (Scheme.Opens.Î¹ _)
      (congr_arg Opens.carrier (ğ’°.map i).opensRange.opensRange_Î¹.symm), Category.assoc,
      P.cancel_left_of_respectsIso]
    exact H _

/-- The intersection of two morphism properties that are local at the target is again local at
the target. -/
instance inf (P Q : MorphismProperty Scheme) [IsLocalAtSource P] [IsLocalAtSource Q] :
    IsLocalAtSource (P âŠ“ Q) where
  iff_of_openCover' {_ _} f ğ’° :=
    âŸ¨fun h i â†¦ âŸ¨(iff_of_openCover' f ğ’°).mp h.left i, (iff_of_openCover' f ğ’°).mp h.right iâŸ©,
     fun h â†¦ âŸ¨(iff_of_openCover' f ğ’°).mpr (fun i â†¦ (h i).left),
      (iff_of_openCover' f ğ’°).mpr (fun i â†¦ (h i).right)âŸ©âŸ©

variable {P} [IsLocalAtSource P]
variable {X Y : Scheme.{u}} {f : X âŸ¶ Y} (ğ’° : X.OpenCover)

lemma comp {UX : Scheme.{u}} (H : P f) (i : UX âŸ¶ X) [IsOpenImmersion i] :
    P (i â‰« f) :=
  (iff_of_openCover' f (X.affineCover.add i)).mp H .none

/-- If `P` is local at the source, then it respects composition on the left with open immersions. -/
instance respectsLeft_isOpenImmersion {P : MorphismProperty Scheme}
    [IsLocalAtSource P] : P.RespectsLeft @IsOpenImmersion where
  precomp i _ _ hf := IsLocalAtSource.comp hf i

lemma of_iSup_eq_top {Î¹} (U : Î¹ â†’ X.Opens) (hU : iSup U = âŠ¤)
    (H : âˆ€ i, P ((U i).Î¹ â‰« f)) : P f := by
  refine (iff_of_openCover' f
    (X.openCoverOfISupEqTop (s := Set.range U) Subtype.val (by ext; simp [â† hU]))).mpr fun i â†¦ ?_
  obtain âŸ¨_, i, rflâŸ© := i
  exact H i

theorem iff_of_iSup_eq_top {Î¹} (U : Î¹ â†’ X.Opens) (hU : iSup U = âŠ¤) :
    P f â†” âˆ€ i, P ((U i).Î¹ â‰« f) :=
  âŸ¨fun H _ â†¦ comp H _, of_iSup_eq_top U hUâŸ©

lemma of_openCover (H : âˆ€ i, P (ğ’°.map i â‰« f)) : P f := by
  refine of_iSup_eq_top (fun i â†¦ (ğ’°.map i).opensRange) ğ’°.iSup_opensRange fun i â†¦ ?_
  rw [â† IsOpenImmersion.isoOfRangeEq_inv_fac (ğ’°.map i) (Scheme.Opens.Î¹ _)
    (congr_arg Opens.carrier (ğ’°.map i).opensRange.opensRange_Î¹.symm), Category.assoc,
    P.cancel_left_of_respectsIso]
  exact H i

theorem iff_of_openCover :
    P f â†” âˆ€ i, P (ğ’°.map i â‰« f) :=
  âŸ¨fun H _ â†¦ comp H _, of_openCover _âŸ©

variable (f) in
lemma of_isOpenImmersion [P.ContainsIdentities] [IsOpenImmersion f] : P f :=
  Category.comp_id f â–¸ comp (P.id_mem Y) f

lemma isLocalAtTarget [P.IsMultiplicative]
    (hP : âˆ€ {X Y Z : Scheme.{u}} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [IsOpenImmersion g], P (f â‰« g) â†’ P f) :
    IsLocalAtTarget P where
  iff_of_openCover' {X Y} f ğ’° := by
    refine (iff_of_openCover (ğ’°.pullbackCover f)).trans (forall_congr' fun i â†¦ ?_)
    rw [â† Scheme.Cover.pullbackHom_map]
    constructor
    Â· exact hP _ _
    Â· exact fun H â†¦ P.comp_mem _ _ H (of_isOpenImmersion _)

lemma sigmaDesc {X : Scheme.{u}} {Î¹ : Type v} [Small.{u} Î¹] {Y : Î¹ â†’ Scheme.{u}}
    {f : âˆ€ i, Y i âŸ¶ X} (hf : âˆ€ i, P (f i)) : P (Sigma.desc f) := by
  rw [IsLocalAtSource.iff_of_openCover (P := P) (Scheme.IsLocallyDirected.openCover _)]
  exact fun i â†¦ by simp [hf]

instance top : IsLocalAtSource (âŠ¤ : MorphismProperty Scheme.{u}) where
  iff_of_openCover' := by simp

section IsLocalAtSourceAndTarget

/-- If `P` is local at the source and the target, then restriction on both source and target
preserves `P`. -/
lemma resLE [IsLocalAtTarget P] {U : Y.Opens} {V : X.Opens} (e : V â‰¤ f â»Â¹áµ U)
    (hf : P f) : P (f.resLE U V e) :=
  IsLocalAtSource.comp (IsLocalAtTarget.restrict hf U) _

/-- If `P` is local at the source, local at the target and is stable under post-composition with
open immersions, then `P` can be checked locally around points. -/
lemma iff_exists_resLE [IsLocalAtTarget P] [P.RespectsRight @IsOpenImmersion] :
    P f â†” âˆ€ x : X, âˆƒ (U : Y.Opens) (V : X.Opens) (_ : x âˆˆ V.1) (e : V â‰¤ f â»Â¹áµ U),
      P (f.resLE U V e) := by
  refine âŸ¨fun hf x â†¦ âŸ¨âŠ¤, âŠ¤, trivial, by simp, resLE _ hfâŸ©, fun hf â†¦ ?_âŸ©
  choose U V hxU e hf using hf
  rw [IsLocalAtSource.iff_of_iSup_eq_top (fun x : X â†¦ V x) (P := P)]
  Â· intro x
    rw [â† Scheme.Hom.resLE_comp_Î¹ _ (e x)]
    exact MorphismProperty.RespectsRight.postcomp (Q := @IsOpenImmersion) _ inferInstance _ (hf x)
  Â· rw [eq_top_iff]
    rintro x -
    simp only [Opens.coe_iSup, Set.mem_iUnion, SetLike.mem_coe]
    use x, hxU x

end IsLocalAtSourceAndTarget

end IsLocalAtSource

/-- An `AffineTargetMorphismProperty` is a class of morphisms from an arbitrary scheme into an
affine scheme. -/
def AffineTargetMorphismProperty :=
  âˆ€ â¦ƒX Y : Schemeâ¦„ (_ : X âŸ¶ Y) [IsAffine Y], Prop

namespace AffineTargetMorphismProperty

@[ext]
lemma ext {P Q : AffineTargetMorphismProperty}
    (H : âˆ€ â¦ƒX Y : Schemeâ¦„ (f : X âŸ¶ Y) [IsAffine Y], P f â†” Q f) : P = Q := by
  delta AffineTargetMorphismProperty; ext; exact H _

/-- The restriction of a `MorphismProperty Scheme` to morphisms with affine target. -/
def of (P : MorphismProperty Scheme) : AffineTargetMorphismProperty :=
  fun _ _ f _ â†¦ P f

/-- An `AffineTargetMorphismProperty` can be extended to a `MorphismProperty` such that it
*never* holds when the target is not affine -/
def toProperty (P : AffineTargetMorphismProperty) :
    MorphismProperty Scheme := fun _ _ f => âˆƒ h, @P _ _ f h

theorem toProperty_apply (P : AffineTargetMorphismProperty)
    {X Y : Scheme} (f : X âŸ¶ Y) [i : IsAffine Y] : P.toProperty f â†” P f := by
  delta AffineTargetMorphismProperty.toProperty; simp [*]

theorem cancel_left_of_respectsIso
    (P : AffineTargetMorphismProperty) [P.toProperty.RespectsIso]
    {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [IsIso f] [IsAffine Z] : P (f â‰« g) â†” P g := by
  rw [â† P.toProperty_apply, â† P.toProperty_apply, P.toProperty.cancel_left_of_respectsIso]

theorem cancel_right_of_respectsIso
    (P : AffineTargetMorphismProperty) [P.toProperty.RespectsIso]
    {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [IsIso g] [IsAffine Z] [IsAffine Y] :
    P (f â‰« g) â†” P f := by rw [â† P.toProperty_apply, â† P.toProperty_apply,
      P.toProperty.cancel_right_of_respectsIso]

theorem arrow_mk_iso_iff
    (P : AffineTargetMorphismProperty) [P.toProperty.RespectsIso]
    {X Y X' Y' : Scheme} {f : X âŸ¶ Y} {f' : X' âŸ¶ Y'}
    (e : Arrow.mk f â‰… Arrow.mk f') {h : IsAffine Y} :
    letI : IsAffine Y' := .of_isIso (Y := Y) e.inv.right
    P f â†” P f' := by
  rw [â† P.toProperty_apply, â† P.toProperty_apply, P.toProperty.arrow_mk_iso_iff e]

theorem respectsIso_mk {P : AffineTargetMorphismProperty}
    (hâ‚ : âˆ€ {X Y Z} (e : X â‰… Y) (f : Y âŸ¶ Z) [IsAffine Z], P f â†’ P (e.hom â‰« f))
    (hâ‚‚ : âˆ€ {X Y Z} (e : Y â‰… Z) (f : X âŸ¶ Y) [IsAffine Y],
      P f â†’ @P _ _ (f â‰« e.hom) (.of_isIso e.inv)) :
    P.toProperty.RespectsIso := by
  apply MorphismProperty.RespectsIso.mk
  Â· rintro X Y Z e f âŸ¨a, hâŸ©; exact âŸ¨a, hâ‚ e f hâŸ©
  Â· rintro X Y Z e f âŸ¨a, hâŸ©; exact âŸ¨.of_isIso e.inv, hâ‚‚ e f hâŸ©

instance respectsIso_of
    (P : MorphismProperty Scheme) [P.RespectsIso] :
    (of P).toProperty.RespectsIso := by
  apply respectsIso_mk
  Â· intro _ _ _ _ _ _; apply MorphismProperty.RespectsIso.precomp
  Â· intro _ _ _ _ _ _; apply MorphismProperty.RespectsIso.postcomp

/-- We say that `P : AffineTargetMorphismProperty` is a local property if
1. `P` respects isomorphisms.
2. If `P` holds for `f : X âŸ¶ Y`, then `P` holds for `f âˆ£_ Y.basicOpen r` for any
  global section `r`.
3. If `P` holds for `f âˆ£_ Y.basicOpen r` for all `r` in a spanning set of the global sections,
  then `P` holds for `f`.
-/
class IsLocal (P : AffineTargetMorphismProperty) : Prop where
  /-- `P` as a morphism property respects isomorphisms -/
  respectsIso : P.toProperty.RespectsIso
  /-- `P` is stable under restriction to basic open set of global sections. -/
  to_basicOpen :
    âˆ€ {X Y : Scheme} [IsAffine Y] (f : X âŸ¶ Y) (r : Î“(Y, âŠ¤)), P f â†’ P (f âˆ£_ Y.basicOpen r)
  /-- `P` for `f` if `P` holds for `f` restricted to basic sets of a spanning set of the global
    sections -/
  of_basicOpenCover :
    âˆ€ {X Y : Scheme} [IsAffine Y] (f : X âŸ¶ Y) (s : Finset Î“(Y, âŠ¤))
      (_ : Ideal.span (s : Set Î“(Y, âŠ¤)) = âŠ¤), (âˆ€ r : s, P (f âˆ£_ Y.basicOpen r.1)) â†’ P f

attribute [instance] AffineTargetMorphismProperty.IsLocal.respectsIso

open AffineTargetMorphismProperty in
instance (P : MorphismProperty Scheme) [IsLocalAtTarget P] : (of P).IsLocal where
  respectsIso := inferInstance
  to_basicOpen _ _ H := IsLocalAtTarget.restrict H _
  of_basicOpenCover {_ Y} _ _ _ hs := IsLocalAtTarget.of_iSup_eq_top _
    (((isAffineOpen_top Y).basicOpen_union_eq_self_iff _).mpr hs)

/-- A `P : AffineTargetMorphismProperty` is stable under base change if `P` holds for `Y âŸ¶ S`
implies that `P` holds for `X Ã—â‚› Y âŸ¶ X` with `X` and `S` affine schemes. -/
def IsStableUnderBaseChange (P : AffineTargetMorphismProperty) : Prop :=
  âˆ€ â¦ƒZ X Y S : Schemeâ¦„ [IsAffine S] [IsAffine X] {f : X âŸ¶ S} {g : Y âŸ¶ S}
    {f' : Z âŸ¶ Y} {g' : Z âŸ¶ X}, IsPullback g' f' f g â†’ P g â†’ P g'

lemma IsStableUnderBaseChange.mk (P : AffineTargetMorphismProperty) [P.toProperty.RespectsIso]
    (H : âˆ€ â¦ƒX Y S : Schemeâ¦„ [IsAffine S] [IsAffine X] (f : X âŸ¶ S) (g : Y âŸ¶ S),
      P g â†’ P (pullback.fst f g)) : P.IsStableUnderBaseChange := by
  intros Z X Y S _ _ f g f' g' h hg
  rw [â† P.cancel_left_of_respectsIso h.isoPullback.inv, h.isoPullback_inv_fst]
  exact H f g hg

end AffineTargetMorphismProperty

section targetAffineLocally

/-- For a `P : AffineTargetMorphismProperty`, `targetAffineLocally P` holds for
`f : X âŸ¶ Y` whenever `P` holds for the restriction of `f` on every affine open subset of `Y`. -/
def targetAffineLocally (P : AffineTargetMorphismProperty) : MorphismProperty Scheme :=
  fun {X Y : Scheme} (f : X âŸ¶ Y) => âˆ€ U : Y.affineOpens, P (f âˆ£_ U)

theorem of_targetAffineLocally_of_isPullback
    {P : AffineTargetMorphismProperty} [P.IsLocal]
    {X Y UX UY : Scheme.{u}} [IsAffine UY] {f : X âŸ¶ Y} {iY : UY âŸ¶ Y} [IsOpenImmersion iY]
    {iX : UX âŸ¶ X} {f' : UX âŸ¶ UY} (h : IsPullback iX f' f iY) (hf : targetAffineLocally P f) :
    P f' := by
  rw [â† P.cancel_left_of_respectsIso h.isoPullback.inv, h.isoPullback_inv_snd]
  exact (P.arrow_mk_iso_iff
    (morphismRestrictOpensRange f _)).mp (hf âŸ¨_, isAffineOpen_opensRange iYâŸ©)

instance (P : AffineTargetMorphismProperty) [P.toProperty.RespectsIso] :
    (targetAffineLocally P).RespectsIso := by
  apply MorphismProperty.RespectsIso.mk
  Â· introv H U
    rw [morphismRestrict_comp, P.cancel_left_of_respectsIso]
    exact H U
  Â· introv H
    rintro âŸ¨U, hU : IsAffineOpen UâŸ©; dsimp
    haveI : IsAffine _ := hU.preimage_of_isIso e.hom
    rw [morphismRestrict_comp, P.cancel_right_of_respectsIso]
    exact H âŸ¨(Opens.map e.hom.base).obj U, hU.preimage_of_isIso e.homâŸ©

/--
`HasAffineProperty P Q` is a type class asserting that `P` is local at the target, and over affine
schemes, it is equivalent to `Q : AffineTargetMorphismProperty`.
To make the proofs easier, we state it instead as
1. `Q` is local at the target
2. `P f` if and only if `âˆ€ U, Q (f âˆ£_ U)` ranging over all affine opens of `U`.
See `HasAffineProperty.iff`.
-/
class HasAffineProperty (P : MorphismProperty Scheme)
    (Q : outParam AffineTargetMorphismProperty) : Prop where
  isLocal_affineProperty : Q.IsLocal
  eq_targetAffineLocally' : P = targetAffineLocally Q

namespace HasAffineProperty

variable (P : MorphismProperty Scheme) {Q} [HasAffineProperty P Q]
variable {X Y : Scheme.{u}} {f : X âŸ¶ Y}

instance (Q : AffineTargetMorphismProperty) [Q.IsLocal] :
    HasAffineProperty (targetAffineLocally Q) Q :=
  âŸ¨inferInstance, rflâŸ©

lemma eq_targetAffineLocally : P = targetAffineLocally Q := eq_targetAffineLocally'

/-- Every property local at the target can be associated with an affine target property.
This is not an instance as the associated property can often take on simpler forms. -/
lemma of_isLocalAtTarget (P) [IsLocalAtTarget P] :
    HasAffineProperty P (AffineTargetMorphismProperty.of P) where
  isLocal_affineProperty := inferInstance
  eq_targetAffineLocally' := by
    ext X Y f
    constructor
    Â· intro hf âŸ¨U, hUâŸ©
      exact IsLocalAtTarget.restrict hf _
    Â· intro hf
      exact IsLocalAtTarget.of_openCover (P := P) Y.affineCover
        fun i â†¦ of_targetAffineLocally_of_isPullback (.of_hasPullback _ _) hf

lemma copy {P P'} {Q Q'} [HasAffineProperty P Q]
    (e : P = P') (e' : Q = Q') : HasAffineProperty P' Q' where
  isLocal_affineProperty := e' â–¸ isLocal_affineProperty P
  eq_targetAffineLocally' := e' â–¸ e.symm â–¸ eq_targetAffineLocally P

variable {P}

theorem of_isPullback {UX UY : Scheme.{u}} [IsAffine UY] {iY : UY âŸ¶ Y} [IsOpenImmersion iY]
    {iX : UX âŸ¶ X} {f' : UX âŸ¶ UY} (h : IsPullback iX f' f iY) (hf : P f) :
    Q f' :=
  letI := isLocal_affineProperty P
  of_targetAffineLocally_of_isPullback h (eq_targetAffineLocally (P := P) â–¸ hf)

theorem restrict (hf : P f) (U : Y.affineOpens) :
    Q (f âˆ£_ U) :=
  of_isPullback (isPullback_morphismRestrict f U).flip hf

instance (priority := 900) : P.RespectsIso := by
  letI := isLocal_affineProperty P
  rw [eq_targetAffineLocally P]
  infer_instance

theorem of_iSup_eq_top
    {Î¹} (U : Î¹ â†’ Y.affineOpens) (hU : â¨† i, (U i : Y.Opens) = âŠ¤)
    (hU' : âˆ€ i, Q (f âˆ£_ U i)) :
    P f := by
  letI := isLocal_affineProperty P
  rw [eq_targetAffineLocally P]
  classical
  intro V
  induction V using of_affine_open_cover U hU  with
  | basicOpen U r h =>
    haveI : IsAffine _ := U.2
    have := AffineTargetMorphismProperty.IsLocal.to_basicOpen (f âˆ£_ U.1) (U.1.topIso.inv r) h
    exact (Q.arrow_mk_iso_iff
      (morphismRestrictRestrictBasicOpen f _ r)).mp this
  | openCover U s hs H =>
    apply AffineTargetMorphismProperty.IsLocal.of_basicOpenCover _
      (s.image (Scheme.Opens.topIso _).inv) (by simp [â† Ideal.map_span, hs, Ideal.map_top])
    intro âŸ¨r, hrâŸ©
    obtain âŸ¨r, hr', rflâŸ© := Finset.mem_image.mp hr
    exact (Q.arrow_mk_iso_iff
      (morphismRestrictRestrictBasicOpen f _ r).symm).mp (H âŸ¨r, hr'âŸ©)
  | hU i => exact hU' i

theorem iff_of_iSup_eq_top
    {Î¹} (U : Î¹ â†’ Y.affineOpens) (hU : â¨† i, (U i : Y.Opens) = âŠ¤) :
    P f â†” âˆ€ i, Q (f âˆ£_ U i) :=
  âŸ¨fun H _ â†¦ restrict H _, fun H â†¦ HasAffineProperty.of_iSup_eq_top U hU HâŸ©

theorem of_openCover
    (ğ’° : Y.OpenCover) [âˆ€ i, IsAffine (ğ’°.obj i)] (hğ’° : âˆ€ i, Q (ğ’°.pullbackHom f i)) :
    P f :=
  letI := isLocal_affineProperty P
  of_iSup_eq_top
    (fun i â†¦ âŸ¨_, isAffineOpen_opensRange (ğ’°.map i)âŸ©) ğ’°.iSup_opensRange
    (fun i â†¦ (Q.arrow_mk_iso_iff (morphismRestrictOpensRange f _)).mpr (hğ’° i))

theorem iff_of_openCover (ğ’° : Y.OpenCover) [âˆ€ i, IsAffine (ğ’°.obj i)] :
    P f â†” âˆ€ i, Q (ğ’°.pullbackHom f i) := by
  letI := isLocal_affineProperty P
  rw [iff_of_iSup_eq_top (P := P)
    (fun i â†¦ âŸ¨_, isAffineOpen_opensRange _âŸ©) ğ’°.iSup_opensRange]
  exact forall_congr' fun i â†¦ Q.arrow_mk_iso_iff
    (morphismRestrictOpensRange f _)

theorem iff_of_isAffine [IsAffine Y] : P f â†” Q f := by
  letI := isLocal_affineProperty P
  haveI : âˆ€ i, IsAffine (Scheme.Cover.obj
      (Scheme.coverOfIsIso (P := @IsOpenImmersion) (ğŸ™ Y)) i) := fun i => by
    dsimp; infer_instance
  rw [iff_of_openCover (P := P) (Scheme.coverOfIsIso.{0} (ğŸ™ Y))]
  trans Q (pullback.snd f (ğŸ™ _))
  Â· exact âŸ¨fun H => H PUnit.unit, fun H _ => HâŸ©
  rw [â† Category.comp_id (pullback.snd _ _), â† pullback.condition,
    Q.cancel_left_of_respectsIso]

instance (priority := 900) : IsLocalAtTarget P := by
  letI := isLocal_affineProperty P
  apply IsLocalAtTarget.mk'
  Â· rw [eq_targetAffineLocally P]
    intro X Y f U H V
    rw [Q.arrow_mk_iso_iff (morphismRestrictRestrict f _ _)]
    exact H âŸ¨_, V.2.image_of_isOpenImmersion (Y.ofRestrict _)âŸ©
  Â· rintro X Y f Î¹ U hU H
    let ğ’° := Y.openCoverOfISupEqTop U hU
    apply of_openCover ğ’°.affineRefinement.openCover
    rintro âŸ¨i, jâŸ©
    have : P (ğ’°.pullbackHom f i) := by
      refine (P.arrow_mk_iso_iff
        (morphismRestrictEq _ ?_ â‰ªâ‰« morphismRestrictOpensRange f (ğ’°.map i))).mp (H i)
      exact (Scheme.Opens.opensRange_Î¹ _).symm
    rw [â† Q.cancel_left_of_respectsIso (ğ’°.pullbackCoverAffineRefinementObjIso f _).inv,
      ğ’°.pullbackCoverAffineRefinementObjIso_inv_pullbackHom]
    exact of_isPullback (.of_hasPullback _ _) this

open AffineTargetMorphismProperty in
protected theorem iff {P : MorphismProperty Scheme} {Q : AffineTargetMorphismProperty} :
    HasAffineProperty P Q â†” IsLocalAtTarget P âˆ§ Q = of P :=
  âŸ¨fun _ â†¦ âŸ¨inferInstance, ext fun _ _ _ â†¦ iff_of_isAffine.symmâŸ©,
    fun âŸ¨_, eâŸ© â†¦ e â–¸ of_isLocalAtTarget PâŸ©

private theorem pullback_fst_of_right (hP' : Q.IsStableUnderBaseChange)
    {X Y S : Scheme} (f : X âŸ¶ S) (g : Y âŸ¶ S) [IsAffine S] (H : Q g) :
    P (pullback.fst f g) := by
  letI := isLocal_affineProperty P
  rw [iff_of_openCover (P := P) X.affineCover]
  intro i
  let e := pullbackSymmetry _ _ â‰ªâ‰« pullbackRightPullbackFstIso f g (X.affineCover.map i)
  have : e.hom â‰« pullback.fst _ _ = X.affineCover.pullbackHom (pullback.fst _ _) i := by
    simp [e, Scheme.Cover.pullbackHom]
  rw [â† this, Q.cancel_left_of_respectsIso]
  apply hP' (.of_hasPullback _ _)
  exact H

theorem isStableUnderBaseChange (hP' : Q.IsStableUnderBaseChange) :
    P.IsStableUnderBaseChange :=
  MorphismProperty.IsStableUnderBaseChange.mk'
    (fun X Y S f g _ H => by
      rw [IsLocalAtTarget.iff_of_openCover (P := P) (S.affineCover.pullbackCover f)]
      intro i
      let e : pullback (pullback.fst f g) ((S.affineCover.pullbackCover f).map i) â‰…
          _ := by
        refine pullbackSymmetry _ _ â‰ªâ‰« pullbackRightPullbackFstIso f g _ â‰ªâ‰« ?_ â‰ªâ‰«
          (pullbackRightPullbackFstIso (S.affineCover.map i) g
            (pullback.snd f (S.affineCover.map i))).symm
        exact asIso
          (pullback.map _ _ _ _ (ğŸ™ _) (ğŸ™ _) (ğŸ™ _) (by simpa using pullback.condition) (by simp))
      have : e.hom â‰« pullback.fst _ _ =
          (S.affineCover.pullbackCover f).pullbackHom (pullback.fst _ _) i := by
        simp [e, Scheme.Cover.pullbackHom]
      rw [â† this, P.cancel_left_of_respectsIso]
      apply HasAffineProperty.pullback_fst_of_right hP'
      letI := isLocal_affineProperty P
      rw [â† pullbackSymmetry_hom_comp_snd, Q.cancel_left_of_respectsIso]
      apply of_isPullback (.of_hasPullback _ _) H)

lemma isLocalAtSource
    (H : âˆ€ {X Y : Scheme.{u}} (f : X âŸ¶ Y) [IsAffine Y] (ğ’° : Scheme.OpenCover.{u} X),
        Q f â†” âˆ€ i, Q (ğ’°.map i â‰« f)) : IsLocalAtSource P where
  iff_of_openCover' {X Y} f ğ’° := by
    simp_rw [IsLocalAtTarget.iff_of_iSup_eq_top _ (iSup_affineOpens_eq_top Y)]
    rw [forall_comm]
    refine forall_congr' fun U â†¦ ?_
    simp_rw [HasAffineProperty.iff_of_isAffine, morphismRestrict_comp]
    exact @H _ _ (f âˆ£_ U.1) U.2 (ğ’°.restrict (f â»Â¹áµ U.1))

end HasAffineProperty

end targetAffineLocally

open MorphismProperty

lemma hasOfPostcompProperty_isOpenImmersion_of_morphismRestrict (P : MorphismProperty Scheme)
    [P.RespectsIso] (H : âˆ€ {X Y : Scheme.{u}} (f : X âŸ¶ Y) (U : Y.Opens), P f â†’ P (f âˆ£_ U)) :
    P.HasOfPostcompProperty @IsOpenImmersion where
  of_postcomp {X Y Z} f g hg hfg := by
    have : (f â‰« g) â»Â¹áµ g.opensRange = âŠ¤ := by simp
    have : f = X.topIso.inv â‰« (X.isoOfEq this).inv â‰« (f â‰« g) âˆ£_ g.opensRange â‰«
        (IsOpenImmersion.isoOfRangeEq g.opensRange.Î¹ g (by simp)).hom := by
      simp [â† cancel_mono g]
    simp_rw [this, cancel_left_of_respectsIso (P := P), cancel_right_of_respectsIso (P := P)]
    exact H _ _ hfg

instance (P : MorphismProperty Scheme) [P.IsStableUnderBaseChange] :
    P.HasOfPostcompProperty @IsOpenImmersion :=
  HasOfPostcompProperty.of_le P (.monomorphisms Scheme) (fun _ _ f _ â†¦ inferInstanceAs (Mono f))

end AlgebraicGeometry
