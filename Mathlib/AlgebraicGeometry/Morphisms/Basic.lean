/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.AffineScheme
import Mathlib.AlgebraicGeometry.Pullbacks
import Mathlib.CategoryTheory.MorphismProperty.Limits
import Mathlib.Data.List.TFAE

#align_import algebraic_geometry.morphisms.basic from "leanprover-community/mathlib"@"434e2fd21c1900747afc6d13d8be7f4eedba7218"

/-!
# Properties of morphisms between Schemes

We provide the basic framework for talking about properties of morphisms between Schemes.

A `MorphismProperty Scheme` is a predicate on morphisms between schemes, and an
`AffineTargetMorphismProperty` is a predicate on morphisms into affine schemes. Given a
`P : AffineTargetMorphismProperty`, we may construct a `MorphismProperty` called
`targetAffineLocally P` that holds for `f : X ‚ü∂ Y` whenever `P` holds for the
restriction of `f` on every affine open subset of `Y`.

## Main definitions

- `AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal`: We say that `P.IsLocal` if `P`
satisfies the assumptions of the affine communication lemma
(`AlgebraicGeometry.of_affine_open_cover`). That is,
1. `P` respects isomorphisms.
2. If `P` holds for `f : X ‚ü∂ Y`, then `P` holds for `f ‚à£_ Y.basicOpen r` for any
  global section `r`.
3. If `P` holds for `f ‚à£_ Y.basicOpen r` for all `r` in a spanning set of the global sections,
  then `P` holds for `f`.

- `AlgebraicGeometry.IsLocalAtTarget`: We say that `IsLocalAtTarget P` for
`P : MorphismProperty Scheme` if
1. `P` respects isomorphisms.
2. If `P` holds for `f : X ‚ü∂ Y`, then `P` holds for `f ‚à£_ U` for any `U`.
3. If `P` holds for `f ‚à£_ U` for an open cover `U` of `Y`, then `P` holds for `f`.

## Main results

- `AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal.affine_openCover_TFAE`:
  If `P.IsLocal`, then `targetAffineLocally P f` iff there exists an affine cover `{ U·µ¢ }` of `Y`
  such that `P` holds for `f ‚à£_ U·µ¢`.
- `AlgebraicGeometry.AffineTargetMorphismProperty.isLocalOfOpenCoverImply`:
  If the existence of an affine cover `{ U·µ¢ }` of `Y` such that `P` holds for `f ‚à£_ U·µ¢` implies
  `targetAffineLocally P f`, then `P.IsLocal`.
- `AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal.affine_target_iff`:
  If `Y` is affine and `f : X ‚ü∂ Y`, then `targetAffineLocally P f ‚Üî P f` provided `P.IsLocal`.
- `AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal.targetAffineLocally_isLocal` :
  If `P.IsLocal`, then `IsLocalAtTarget (targetAffineLocally P)`.
- `AlgebraicGeometry.IsLocalAtTarget.openCover_TFAE`:
  If `IsLocalAtTarget P`, then `P f` iff there exists an open cover `{ U·µ¢ }` of `Y`
  such that `P` holds for `f ‚à£_ U·µ¢`.

These results should not be used directly, and should be ported to each property that is local.

-/

set_option linter.uppercaseLean3 false

universe u

open TopologicalSpace CategoryTheory CategoryTheory.Limits Opposite

noncomputable section

namespace AlgebraicGeometry

section IsLocalAtTarget

/--
We say that `P : MorphismProperty Scheme` is local at the target if
1. `P` respects isomorphisms.
2. If `P` holds for `f : X ‚ü∂ Y`, then `P` holds for `f ‚à£_ U` for any `U`.
3. If `P` holds for `f ‚à£_ U` for an open cover `U` of `Y`, then `P` holds for `f`.
-/
class IsLocalAtTarget (P : MorphismProperty Scheme) : Prop where
  /-- `P` respects isomorphisms. -/
  respectsIso : P.RespectsIso
  /-- If `P` holds for `f : X ‚ü∂ Y`, then `P` holds for `f ‚à£_ U` for any `U`. -/
  restrict : ‚àÄ {X Y : Scheme} (f : X ‚ü∂ Y) (U : Opens Y), P f ‚Üí P (f ‚à£_ U)
  /-- If `P` holds for `f ‚à£_ U` for an open cover `U` of `Y`, then `P` holds for `f`.  -/
  of_sSup_eq_top :
    ‚àÄ {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (Us : Set (Opens Y)), sSup Us = ‚ä§ ‚Üí
      (‚àÄ U ‚àà Us, P (f ‚à£_ U)) ‚Üí P f
#align algebraic_geometry.property_is_local_at_target AlgebraicGeometry.IsLocalAtTarget

attribute [instance] IsLocalAtTarget.respectsIso

variable {P} [hP : IsLocalAtTarget P]
variable {X Y U : Scheme.{u}} (f : X ‚ü∂ Y) (g : U ‚ü∂ Y) [IsOpenImmersion g] (ùí∞ : Y.OpenCover)

lemma IsLocalAtTarget.baseChange (H : P f) :
    P (pullback.snd : pullback f g ‚ü∂ U) :=
  (P.arrow_mk_iso_iff
    (morphismRestrictOpensRange f _)).mp (restrict f g.opensRange H)

lemma IsLocalAtTarget.of_iSup_eq_top {Œπ} (U : Œπ ‚Üí Opens Y) (hU : iSup U = ‚ä§)
    (H : ‚àÄ i, P (f ‚à£_ U i)) : P f :=
  of_sSup_eq_top f (Set.range U) hU (fun _ ‚ü®j, e‚ü© ‚Ü¶ e ‚ñ∏ H j)

theorem IsLocalAtTarget.iff_of_iSup_eq_top {Œπ} (U : Œπ ‚Üí Opens Y) (hU : iSup U = ‚ä§) :
    P f ‚Üî ‚àÄ i, P (f ‚à£_ U i) :=
  ‚ü®fun H _ ‚Ü¶ restrict _ _ H, of_iSup_eq_top _ U hU‚ü©

lemma IsLocalAtTarget.of_openCover (H : ‚àÄ i, P (ùí∞.pullbackHom f i)) : P f := by
  apply of_iSup_eq_top f (fun i ‚Ü¶ (ùí∞.map i).opensRange) ùí∞.iSup_opensRange
  exact fun i ‚Ü¶ (P.arrow_mk_iso_iff (morphismRestrictOpensRange f _)).mpr (H i)

theorem IsLocalAtTarget.iff_of_openCover (ùí∞ : Y.OpenCover) :
    P f ‚Üî ‚àÄ i, P (ùí∞.pullbackHom f i) :=
  ‚ü®fun H _ ‚Ü¶ baseChange _ _ H, of_openCover _ _‚ü©

end IsLocalAtTarget

section AffineTargetMorphismProperty

/-- An `AffineTargetMorphismProperty` is a class of morphisms from an arbitrary scheme into an
affine scheme. -/
def AffineTargetMorphismProperty :=
  ‚àÄ ‚¶ÉX Y : Scheme‚¶Ñ (_ : X ‚ü∂ Y) [IsAffine Y], Prop
#align algebraic_geometry.affine_target_morphism_property AlgebraicGeometry.AffineTargetMorphismProperty

/-- An `AffineTargetMorphismProperty` is a class of morphisms from an arbitrary scheme into an
affine scheme. -/
def AffineTargetMorphismProperty.of (P : MorphismProperty Scheme) : AffineTargetMorphismProperty :=
  fun _ _ f _ ‚Ü¶ P f

/-- `IsIso` as a `MorphismProperty`. -/
protected def Scheme.isIso : MorphismProperty Scheme :=
  @IsIso Scheme _
#align algebraic_geometry.Scheme.is_iso AlgebraicGeometry.Scheme.isIso

/-- `IsIso` as an `AffineTargetMorphismProperty`. -/
protected def Scheme.affineTargetIsIso : AffineTargetMorphismProperty := fun _ _ f _ => IsIso f
#align algebraic_geometry.Scheme.affine_target_is_iso AlgebraicGeometry.Scheme.affineTargetIsIso

instance : Inhabited AffineTargetMorphismProperty := ‚ü®Scheme.affineTargetIsIso‚ü©

/-- An `AffineTargetMorphismProperty` can be extended to a `MorphismProperty` such that it
*never* holds when the target is not affine -/
def AffineTargetMorphismProperty.toProperty (P : AffineTargetMorphismProperty) :
    MorphismProperty Scheme := fun _ _ f => ‚àÉ h, @P _ _ f h
#align algebraic_geometry.affine_target_morphism_property.to_property AlgebraicGeometry.AffineTargetMorphismProperty.toProperty

theorem AffineTargetMorphismProperty.toProperty_apply (P : AffineTargetMorphismProperty)
    {X Y : Scheme} (f : X ‚ü∂ Y) [i : IsAffine Y] : P.toProperty f ‚Üî P f := by
  delta AffineTargetMorphismProperty.toProperty; simp [*]
#align algebraic_geometry.affine_target_morphism_property.to_property_apply AlgebraicGeometry.AffineTargetMorphismProperty.toProperty_apply

theorem AffineTargetMorphismProperty.cancel_left_of_respectsIso
    (P : AffineTargetMorphismProperty) [P.toProperty.RespectsIso]
    {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsIso f] [IsAffine Z] : P (f ‚â´ g) ‚Üî P g := by
  rw [‚Üê P.toProperty_apply, ‚Üê P.toProperty_apply, P.toProperty.cancel_left_of_respectsIso]
#align algebraic_geometry.affine_cancel_left_is_iso AlgebraicGeometry.AffineTargetMorphismProperty.cancel_left_of_respectsIso

theorem AffineTargetMorphismProperty.cancel_right_of_respectsIso
    (P : AffineTargetMorphismProperty) [P.toProperty.RespectsIso]
    {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsIso g] [IsAffine Z] [IsAffine Y] :
    P (f ‚â´ g) ‚Üî P f := by rw [‚Üê P.toProperty_apply, ‚Üê P.toProperty_apply,
      P.toProperty.cancel_right_of_respectsIso]
#align algebraic_geometry.affine_cancel_right_is_iso AlgebraicGeometry.AffineTargetMorphismProperty.cancel_right_of_respectsIso

@[deprecated (since := "2024-07-02")] alias affine_cancel_left_isIso :=
  AffineTargetMorphismProperty.cancel_left_of_respectsIso
@[deprecated (since := "2024-07-02")] alias affine_cancel_right_isIso :=
  AffineTargetMorphismProperty.cancel_right_of_respectsIso

theorem AffineTargetMorphismProperty.arrow_mk_iso_iff
    (P : AffineTargetMorphismProperty) [P.toProperty.RespectsIso]
    {X Y X' Y' : Scheme} {f : X ‚ü∂ Y} {f' : X' ‚ü∂ Y'}
    (e : Arrow.mk f ‚âÖ Arrow.mk f') {h : IsAffine Y} :
    letI : IsAffine Y' := isAffine_of_isIso (Y := Y) e.inv.right
    P f ‚Üî P f' := by
  rw [‚Üê P.toProperty_apply, ‚Üê P.toProperty_apply, P.toProperty.arrow_mk_iso_iff e]

theorem AffineTargetMorphismProperty.respectsIso_mk {P : AffineTargetMorphismProperty}
    (h‚ÇÅ : ‚àÄ {X Y Z} (e : X ‚âÖ Y) (f : Y ‚ü∂ Z) [IsAffine Z], P f ‚Üí P (e.hom ‚â´ f))
    (h‚ÇÇ : ‚àÄ {X Y Z} (e : Y ‚âÖ Z) (f : X ‚ü∂ Y) [h : IsAffine Y],
      P f ‚Üí @P _ _ (f ‚â´ e.hom) (isAffine_of_isIso e.inv)) :
    P.toProperty.RespectsIso := by
  constructor
  ¬∑ rintro X Y Z e f ‚ü®a, h‚ü©; exact ‚ü®a, h‚ÇÅ e f h‚ü©
  ¬∑ rintro X Y Z e f ‚ü®a, h‚ü©; exact ‚ü®isAffine_of_isIso e.inv, h‚ÇÇ e f h‚ü©
#align algebraic_geometry.affine_target_morphism_property.respects_iso_mk AlgebraicGeometry.AffineTargetMorphismProperty.respectsIso_mk

instance AffineTargetMorphismProperty.respectsIso_of
    (P : MorphismProperty Scheme) [P.RespectsIso] :
    (of P).toProperty.RespectsIso := by
  apply respectsIso_mk
  ¬∑ intro _ _ _ _ _ _; apply MorphismProperty.RespectsIso.precomp
  ¬∑ intro _ _ _ _ _ _; apply MorphismProperty.RespectsIso.postcomp

/-- We say that `P : AffineTargetMorphismProperty` is a local property if
1. `P` respects isomorphisms.
2. If `P` holds for `f : X ‚ü∂ Y`, then `P` holds for `f ‚à£_ Y.basicOpen r` for any
  global section `r`.
3. If `P` holds for `f ‚à£_ Y.basicOpen r` for all `r` in a spanning set of the global sections,
  then `P` holds for `f`.
-/
class AffineTargetMorphismProperty.IsLocal (P : AffineTargetMorphismProperty) : Prop where
  /-- `P` as a morphism property respects isomorphisms -/
  respectsIso : P.toProperty.RespectsIso
  /-- `P` is stable under restriction to basic open set of global sections. -/
  to_basicOpen :
    ‚àÄ {X Y : Scheme} [IsAffine Y] (f : X ‚ü∂ Y) (r : Œì(Y, ‚ä§)),
      P f ‚Üí @P _ _ (f ‚à£_ Y.basicOpen r) ((isAffineOpen_top Y).basicOpen _)
  /-- `P` for `f` if `P` holds for `f` restricted to basic sets of a spanning set of the global
    sections -/
  of_basicOpenCover :
    ‚àÄ {X Y : Scheme} [IsAffine Y] (f : X ‚ü∂ Y) (s : Finset Œì(Y, ‚ä§))
      (_ : Ideal.span (s : Set Œì(Y, ‚ä§)) = ‚ä§),
      (‚àÄ r : s, @P _ _ (f ‚à£_ Y.basicOpen r.1) ((isAffineOpen_top Y).basicOpen _)) ‚Üí P f
#align algebraic_geometry.affine_target_morphism_property.is_local AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal

attribute [instance] AffineTargetMorphismProperty.IsLocal.respectsIso

open AffineTargetMorphismProperty in
instance (P : MorphismProperty Scheme) [IsLocalAtTarget P] :
    (of P).IsLocal where
  respectsIso := inferInstance
  to_basicOpen _ _ := IsLocalAtTarget.restrict _ _
  of_basicOpenCover {_ Y} _ _ _ hs := IsLocalAtTarget.of_iSup_eq_top _ _
    (((isAffineOpen_top Y).basicOpen_union_eq_self_iff _).mpr hs)

/-- A `P : AffineTargetMorphismProperty` is stable under base change if `P` holds for `Y ‚ü∂ S`
implies that `P` holds for `X √ó‚Çõ Y ‚ü∂ X` with `X` and `S` affine schemes. -/
def AffineTargetMorphismProperty.StableUnderBaseChange (P : AffineTargetMorphismProperty) : Prop :=
  ‚àÄ ‚¶ÉX Y S : Scheme‚¶Ñ [IsAffine S] [IsAffine X] (f : X ‚ü∂ S) (g : Y ‚ü∂ S),
    P g ‚Üí P (pullback.fst : pullback f g ‚ü∂ X)
#align algebraic_geometry.affine_target_morphism_property.stable_under_base_change AlgebraicGeometry.AffineTargetMorphismProperty.StableUnderBaseChange

end AffineTargetMorphismProperty

section targetAffineLocally

/-- For a `P : AffineTargetMorphismProperty`, `targetAffineLocally P` holds for
`f : X ‚ü∂ Y` whenever `P` holds for the restriction of `f` on every affine open subset of `Y`. -/
def targetAffineLocally (P : AffineTargetMorphismProperty) : MorphismProperty Scheme :=
  fun {X Y : Scheme} (f : X ‚ü∂ Y) => ‚àÄ U : Y.affineOpens, P (f ‚à£_ U)
#align algebraic_geometry.target_affine_locally AlgebraicGeometry.targetAffineLocally

class HasAffineProperty (P : MorphismProperty Scheme) where
  protected affineProperty : AffineTargetMorphismProperty
  isLocal_affineProperty : affineProperty.IsLocal
  protected eq_targetAffineLocally' : P = targetAffineLocally affineProperty

alias _root_.CategoryTheory.MorphismProperty.affineProperty := HasAffineProperty.affineProperty

variable (P : MorphismProperty Scheme) [HasAffineProperty P]
variable {X Y : Scheme.{u}} (f : X ‚ü∂ Y)

instance : P.affineProperty.IsLocal := HasAffineProperty.isLocal_affineProperty

instance (P : AffineTargetMorphismProperty) [P.IsLocal] :
    HasAffineProperty (targetAffineLocally P) :=
  ‚ü®P, inferInstance, rfl‚ü©

lemma eq_targetAffineLocally : P = targetAffineLocally P.affineProperty :=
    HasAffineProperty.eq_targetAffineLocally'

lemma iff_targetAffineLocally : P f ‚Üî targetAffineLocally P.affineProperty f :=
    eq_targetAffineLocally P ‚ñ∏ Iff.rfl

theorem HasAffineProperty.restrict (hf : P f) (U : Y.affineOpens) :
    P.affineProperty (f ‚à£_ U) :=
  (iff_targetAffineLocally P f).mp hf U

instance (priority := 900) HasAffineProperty.respectsIso_mk :
    P.RespectsIso := by
  rw [eq_targetAffineLocally P]
  constructor
  ¬∑ introv H U
    rw [morphismRestrict_comp, P.affineProperty.cancel_left_of_respectsIso]
    exact H U
  ¬∑ introv H
    rintro ‚ü®U, hU : IsAffineOpen U‚ü©; dsimp
    haveI : IsAffine _ := hU.preimage_of_isIso e.hom
    rw [morphismRestrict_comp, P.affineProperty.cancel_right_of_respectsIso]
    exact H ‚ü®(Opens.map e.hom.val.base).obj U, hU.preimage_of_isIso e.hom‚ü©
#align algebraic_geometry.target_affine_locally_respects_iso AlgebraicGeometry.HasAffineProperty.respectsIso_mk

theorem HasAffineProperty.of_iSup_eq_top
    {Œπ} (U : Œπ ‚Üí Y.affineOpens) (hU : ‚®Ü i, (U i : Opens Y) = ‚ä§)
    (hU' : ‚àÄ i, P.affineProperty (f ‚à£_ U i)) :
    P f := by
  rw [eq_targetAffineLocally P]
  classical
  intro V
  induction V using of_affine_open_cover U hU  with
  | basicOpen U r h =>
    haveI : IsAffine _ := U.2
    have := AffineTargetMorphismProperty.IsLocal.to_basicOpen (f ‚à£_ U.1) ((Y.resTop U).inv r) h
    exact (P.affineProperty.arrow_mk_iso_iff
      (morphismRestrictRestrictBasicOpen f _ r)).mp this
  | openCover U s hs H =>
    apply AffineTargetMorphismProperty.IsLocal.of_basicOpenCover _
      (s.image (Y.resTop _).inv) (by simp [‚Üê Ideal.map_span, hs, Ideal.map_top])
    intro ‚ü®r, hr‚ü©
    obtain ‚ü®r, hr', rfl‚ü© := Finset.mem_image.mp hr
    exact (P.affineProperty.arrow_mk_iso_iff
      (morphismRestrictRestrictBasicOpen f _ r).symm).mp (H ‚ü®r, hr'‚ü©)
  | hU i => exact hU' i

theorem HasAffineProperty.iff_of_iSup_eq_top
    {Œπ} (U : Œπ ‚Üí Y.affineOpens) (hU : ‚®Ü i, (U i : Opens Y) = ‚ä§) :
    P f ‚Üî ‚àÄ i, P.affineProperty (f ‚à£_ U i) :=
  ‚ü®fun H _ ‚Ü¶ restrict P f H _, fun H ‚Ü¶ HasAffineProperty.of_iSup_eq_top P f U hU H‚ü©

theorem HasAffineProperty.of_openCover
    (ùí∞ : Y.OpenCover) [‚àÄ i, IsAffine (ùí∞.obj i)] (hùí∞ : ‚àÄ i, P.affineProperty (ùí∞.pullbackHom f i)) :
    P f := by
  apply of_iSup_eq_top P f
    (fun i ‚Ü¶ ‚ü®_, isAffineOpen_opensRange (ùí∞.map i)‚ü©) ùí∞.iSup_opensRange
    (fun i ‚Ü¶ (P.affineProperty.arrow_mk_iso_iff (morphismRestrictOpensRange f _)).mpr (hùí∞ i))
#align algebraic_geometry.target_affine_locally_of_open_cover AlgebraicGeometry.HasAffineProperty.of_openCover

theorem HasAffineProperty.iff_of_openCover
    (ùí∞ : Y.OpenCover) [‚àÄ i, IsAffine (ùí∞.obj i)] :
    P f ‚Üî ‚àÄ i, P.affineProperty (ùí∞.pullbackHom f i) := by
  rw [iff_of_iSup_eq_top P f
    (fun i ‚Ü¶ ‚ü®_, isAffineOpen_opensRange _‚ü©) ùí∞.iSup_opensRange]
  exact forall_congr' fun i ‚Ü¶ P.affineProperty.arrow_mk_iso_iff
    (morphismRestrictOpensRange f _)

theorem HasAffineProperty.morphsimRestrict
    (U : Opens Y) (H : P f) : P (f ‚à£_ U) := by
  rw [eq_targetAffineLocally P] at H ‚ä¢
  intros V
  rw [P.affineProperty.arrow_mk_iso_iff (morphismRestrictRestrict _ _ _)]
  exact H ‚ü®_, V.2.image_of_isOpenImmersion _‚ü©

protected
theorem HasAffineProperty.pullback {U} (g : U ‚ü∂ Y) [IsOpenImmersion g] (h : P f) :
    P (pullback.snd : pullback f g ‚ü∂ _) := by
  rw [‚Üê P.arrow_mk_iso_iff (morphismRestrictOpensRange _ _)]
  exact morphsimRestrict P f _ h

theorem HasAffineProperty.iff_of_isAffine [IsAffine Y] :
    P f ‚Üî P.affineProperty f := by
  haveI : ‚àÄ i, IsAffine (Scheme.OpenCover.obj (Scheme.openCoverOfIsIso (ùüô Y)) i) := fun i => by
    dsimp; infer_instance
  rw [iff_of_openCover P f (Scheme.openCoverOfIsIso.{0} (ùüô Y))]
  trans P.affineProperty (pullback.snd : pullback f (ùüô _) ‚ü∂ _)
  ¬∑ exact ‚ü®fun H => H PUnit.unit, fun H _ => H‚ü©
  rw [‚Üê Category.comp_id pullback.snd, ‚Üê pullback.condition,
    P.affineProperty.cancel_left_of_respectsIso]
#align algebraic_geometry.affine_target_morphism_property.is_local.affine_target_iff AlgebraicGeometry.HasAffineProperty.iff_of_isAffine

theorem HasAffineProperty.pullback_of_isAffine {U} (g : U ‚ü∂ Y) [IsOpenImmersion g] [IsAffine U]
    (h : P f) : P.affineProperty (pullback.snd : pullback f g ‚ü∂ _) := by
  rw [‚Üê iff_of_isAffine P]
  exact HasAffineProperty.pullback P f _ h

instance (ùí∞ : X.AffineOpenCover) (i) : IsAffine (ùí∞.openCover.obj i) :=
  inferInstanceAs (IsAffine (Spec (ùí∞.obj i)))

instance HasAffineProperty.isLocalAtTarget_mk :
    IsLocalAtTarget P := by
  constructor
  ¬∑ exact respectsIso_mk P
  ¬∑ rw [eq_targetAffineLocally P]
    intro X Y f U H V
    rw [P.affineProperty.arrow_mk_iso_iff (morphismRestrictRestrict f _ _)]
    exact H ‚ü®_, V.2.image_of_isOpenImmersion (Y.ofRestrict _)‚ü©
  ¬∑ rintro X Y f Us hU H
    let ùí∞ := Y.openCoverOfSuprEqTop (fun i : Us ‚Ü¶ i) (by rwa [iSup_subtype, ‚Üê sSup_eq_iSup])
    apply of_openCover P _ ùí∞.affineRefinement.openCover
    rintro ‚ü®i, j‚ü©
    have : P (ùí∞.pullbackHom f i) := by
      refine (P.arrow_mk_iso_iff
        (morphismRestrictEq _ ?_ ‚â™‚â´ morphismRestrictOpensRange f (ùí∞.map i))).mp (H _ i.2)
      exact (opensRange_ŒπOpens _).symm
    rw [‚Üê P.affineProperty.cancel_left_of_respectsIso (ùí∞.pullbackCoverAffineRefinementObj f _).inv,
      ùí∞.pullbackCoverAffineRefinementObj_inv_pullbackHom]
    exact pullback_of_isAffine P _ ((ùí∞.obj i).affineCover.map j) this
#align algebraic_geometry.affine_target_morphism_property.is_local.target_affine_locally_is_local AlgebraicGeometry.HasAffineProperty.isLocalAtTarget_mk

theorem HasAffineProperty.pullback_fst_of_right (hP' : P.affineProperty.StableUnderBaseChange)
    {X Y S : Scheme} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [IsAffine S] (H : P.affineProperty g) :
    P (pullback.fst : pullback f g ‚ü∂ X) := by
  rw [iff_of_openCover P _ X.affineCover]
  intro i
  let e := pullbackSymmetry _ _ ‚â™‚â´ pullbackRightPullbackFstIso f g (X.affineCover.map i)
  have : e.hom ‚â´ pullback.fst = X.affineCover.pullbackHom pullback.fst i := by
    simp [e, Scheme.OpenCover.pullbackHom]
  rw [‚Üê this, P.affineProperty.cancel_left_of_respectsIso]
  apply hP' _ _
  exact H
#align algebraic_geometry.affine_target_morphism_property.is_local.target_affine_locally_pullback_fst_of_right_of_stable_under_base_change AlgebraicGeometry.HasAffineProperty.pullback_fst_of_right

theorem HasAffineProperty.stableUnderBaseChange_mk (hP' : P.affineProperty.StableUnderBaseChange) :
    P.StableUnderBaseChange :=
  MorphismProperty.StableUnderBaseChange.mk
    (fun X Y S f g H => by
      rw [IsLocalAtTarget.iff_of_openCover (P := P) _ (S.affineCover.pullbackCover f)]
      intro i
      let e : pullback (pullback.fst : pullback f g ‚ü∂ _) ((S.affineCover.pullbackCover f).map i) ‚âÖ
          _ := by
        refine pullbackSymmetry _ _ ‚â™‚â´ pullbackRightPullbackFstIso f g _ ‚â™‚â´ ?_ ‚â™‚â´
          (pullbackRightPullbackFstIso (S.affineCover.map i) g
            (pullback.snd : pullback f (S.affineCover.map i) ‚ü∂ _)).symm
        exact asIso
          (pullback.map _ _ _ _ (ùüô _) (ùüô _) (ùüô _) (by simpa using pullback.condition) (by simp))
      have : e.hom ‚â´ pullback.fst =
          (S.affineCover.pullbackCover f).pullbackHom pullback.fst i := by
        simp [e, Scheme.OpenCover.pullbackHom]
      rw [‚Üê this, P.cancel_left_of_respectsIso]
      apply HasAffineProperty.pullback_fst_of_right P hP'
      rw [‚Üê pullbackSymmetry_hom_comp_snd, P.affineProperty.cancel_left_of_respectsIso]
      apply pullback_of_isAffine P _ _ H)
#align algebraic_geometry.affine_target_morphism_property.is_local.stable_under_base_change AlgebraicGeometry.HasAffineProperty.stableUnderBaseChange_mk

end targetAffineLocally

end AlgebraicGeometry
