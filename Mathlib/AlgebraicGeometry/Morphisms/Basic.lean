/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.AffineScheme
import Mathlib.AlgebraicGeometry.Pullbacks
import Mathlib.CategoryTheory.MorphismProperty.Limits
import Mathlib.Data.List.TFAE

#align_import algebraic_geometry.morphisms.basic from "leanprover-community/mathlib"@"434e2fd21c1900747afc6d13d8be7f4eedba7218"

/-!
# Properties of morphisms between Schemes

We provide the basic framework for talking about properties of morphisms between Schemes.

A `MorphismProperty Scheme` is a predicate on morphisms between schemes, and an
`AffineTargetMorphismProperty` is a predicate on morphisms into affine schemes. Given a
`P : AffineTargetMorphismProperty`, we may construct a `MorphismProperty` called
`targetAffineLocally P` that holds for `f : X ‚ü∂ Y` whenever `P` holds for the
restriction of `f` on every affine open subset of `Y`.

## Main definitions

- `AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal`: We say that `P.IsLocal` if `P`
satisfies the assumptions of the affine communication lemma
(`AlgebraicGeometry.of_affine_open_cover`). That is,
1. `P` respects isomorphisms.
2. If `P` holds for `f : X ‚ü∂ Y`, then `P` holds for `f ‚à£_ Y.basicOpen r` for any
  global section `r`.
3. If `P` holds for `f ‚à£_ Y.basicOpen r` for all `r` in a spanning set of the global sections,
  then `P` holds for `f`.

- `AlgebraicGeometry.PropertyIsLocalAtTarget`: We say that `PropertyIsLocalAtTarget P` for
`P : MorphismProperty Scheme` if
1. `P` respects isomorphisms.
2. If `P` holds for `f : X ‚ü∂ Y`, then `P` holds for `f ‚à£_ U` for any `U`.
3. If `P` holds for `f ‚à£_ U` for an open cover `U` of `Y`, then `P` holds for `f`.

## Main results

- `AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal.affine_openCover_TFAE`:
  If `P.IsLocal`, then `targetAffineLocally P f` iff there exists an affine cover `{ U·µ¢ }` of `Y`
  such that `P` holds for `f ‚à£_ U·µ¢`.
- `AlgebraicGeometry.AffineTargetMorphismProperty.isLocalOfOpenCoverImply`:
  If the existence of an affine cover `{ U·µ¢ }` of `Y` such that `P` holds for `f ‚à£_ U·µ¢` implies
  `targetAffineLocally P f`, then `P.IsLocal`.
- `AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal.affine_target_iff`:
  If `Y` is affine and `f : X ‚ü∂ Y`, then `targetAffineLocally P f ‚Üî P f` provided `P.IsLocal`.
- `AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal.targetAffineLocally_isLocal` :
  If `P.IsLocal`, then `PropertyIsLocalAtTarget (targetAffineLocally P)`.
- `AlgebraicGeometry.PropertyIsLocalAtTarget.openCover_TFAE`:
  If `PropertyIsLocalAtTarget P`, then `P f` iff there exists an open cover `{ U·µ¢ }` of `Y`
  such that `P` holds for `f ‚à£_ U·µ¢`.

These results should not be used directly, and should be ported to each property that is local.

-/

set_option linter.uppercaseLean3 false

universe u

open TopologicalSpace CategoryTheory CategoryTheory.Limits Opposite

noncomputable section

namespace AlgebraicGeometry

section PropertyIsLocalAtTarget

/--
We say that `P : MorphismProperty Scheme` is local at the target if
1. `P` respects isomorphisms.
2. If `P` holds for `f : X ‚ü∂ Y`, then `P` holds for `f ‚à£_ U` for any `U`.
3. If `P` holds for `f ‚à£_ U` for an open cover `U` of `Y`, then `P` holds for `f`.
-/
structure PropertyIsLocalAtTarget (P : MorphismProperty Scheme) : Prop where
  /-- `P` respects isomorphisms. -/
  RespectsIso : P.RespectsIso
  /-- If `P` holds for `f : X ‚ü∂ Y`, then `P` holds for `f ‚à£_ U` for any `U`. -/
  restrict : ‚àÄ {X Y : Scheme} (f : X ‚ü∂ Y) (U : Opens Y), P f ‚Üí P (f ‚à£_ U)
  /-- If `P` holds for `f ‚à£_ U` for an open cover `U` of `Y`, then `P` holds for `f`.  -/
  of_sSup_eq_top :
    ‚àÄ {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (Us : Set (Opens Y)), sSup Us = ‚ä§ ‚Üí
      (‚àÄ U ‚àà Us, P (f ‚à£_ U)) ‚Üí P f
#align algebraic_geometry.property_is_local_at_target AlgebraicGeometry.PropertyIsLocalAtTarget

variable {P} (hP : PropertyIsLocalAtTarget P)
variable {X Y U : Scheme.{u}} (f : X ‚ü∂ Y) (g : U ‚ü∂ Y) [IsOpenImmersion g] (ùí∞ : Y.OpenCover)

lemma PropertyIsLocalAtTarget.baseChange (H : P f) :
    P (pullback.snd : pullback f g ‚ü∂ U) :=
  (hP.RespectsIso.arrow_mk_iso_iff
    (morphismRestrictOpensRange f _)).mp (hP.restrict f g.opensRange H)

lemma PropertyIsLocalAtTarget.of_iSup_eq_top {Œπ} (U : Œπ ‚Üí Opens Y) (hU : iSup U = ‚ä§)
    (H : ‚àÄ i, P (f ‚à£_ U i)) : P f :=
  hP.of_sSup_eq_top f (Set.range U) hU (fun _ ‚ü®j, e‚ü© ‚Ü¶ e ‚ñ∏ H j)

theorem PropertyIsLocalAtTarget.iff_of_iSup_eq_top {Œπ} (U : Œπ ‚Üí Opens Y) (hU : iSup U = ‚ä§) :
    P f ‚Üî ‚àÄ i, P (f ‚à£_ U i) :=
  ‚ü®fun H _ ‚Ü¶ hP.restrict _ _ H, of_iSup_eq_top hP _ U hU‚ü©

lemma PropertyIsLocalAtTarget.of_openCover (H : ‚àÄ i, P (ùí∞.pullbackHom f i)) : P f := by
  apply hP.of_iSup_eq_top f (fun i ‚Ü¶ (ùí∞.map i).opensRange) ùí∞.iSup_opensRange
  exact fun i ‚Ü¶ (hP.RespectsIso.arrow_mk_iso_iff (morphismRestrictOpensRange f _)).mpr (H i)

theorem PropertyIsLocalAtTarget.iff_of_openCover (ùí∞ : Y.OpenCover) :
    P f ‚Üî ‚àÄ i, P (ùí∞.pullbackHom f i) :=
  ‚ü®fun H _ ‚Ü¶ hP.baseChange _ _ H, of_openCover hP _ _‚ü©

end PropertyIsLocalAtTarget

section AffineTargetMorphismProperty

/-- An `AffineTargetMorphismProperty` is a class of morphisms from an arbitrary scheme into an
affine scheme. -/
def AffineTargetMorphismProperty :=
  ‚àÄ ‚¶ÉX Y : Scheme‚¶Ñ (_ : X ‚ü∂ Y) [IsAffine Y], Prop
#align algebraic_geometry.affine_target_morphism_property AlgebraicGeometry.AffineTargetMorphismProperty

/-- `IsIso` as a `MorphismProperty`. -/
protected def Scheme.isIso : MorphismProperty Scheme :=
  @IsIso Scheme _
#align algebraic_geometry.Scheme.is_iso AlgebraicGeometry.Scheme.isIso

/-- `IsIso` as an `AffineTargetMorphismProperty`. -/
protected def Scheme.affineTargetIsIso : AffineTargetMorphismProperty := fun _ _ f _ => IsIso f
#align algebraic_geometry.Scheme.affine_target_is_iso AlgebraicGeometry.Scheme.affineTargetIsIso

instance : Inhabited AffineTargetMorphismProperty := ‚ü®Scheme.affineTargetIsIso‚ü©

/-- An `AffineTargetMorphismProperty` can be extended to a `MorphismProperty` such that it
*never* holds when the target is not affine -/
def AffineTargetMorphismProperty.toProperty (P : AffineTargetMorphismProperty) :
    MorphismProperty Scheme := fun _ _ f => ‚àÉ h, @P _ _ f h
#align algebraic_geometry.affine_target_morphism_property.to_property AlgebraicGeometry.AffineTargetMorphismProperty.toProperty

theorem AffineTargetMorphismProperty.toProperty_apply (P : AffineTargetMorphismProperty)
    {X Y : Scheme} (f : X ‚ü∂ Y) [i : IsAffine Y] : P.toProperty f ‚Üî P f := by
  delta AffineTargetMorphismProperty.toProperty; simp [*]
#align algebraic_geometry.affine_target_morphism_property.to_property_apply AlgebraicGeometry.AffineTargetMorphismProperty.toProperty_apply

theorem affine_cancel_left_isIso {P : AffineTargetMorphismProperty} (hP : P.toProperty.RespectsIso)
    {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsIso f] [IsAffine Z] : P (f ‚â´ g) ‚Üî P g := by
  rw [‚Üê P.toProperty_apply, ‚Üê P.toProperty_apply, hP.cancel_left_isIso]
#align algebraic_geometry.affine_cancel_left_is_iso AlgebraicGeometry.affine_cancel_left_isIso

theorem affine_cancel_right_isIso {P : AffineTargetMorphismProperty} (hP : P.toProperty.RespectsIso)
    {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsIso g] [IsAffine Z] [IsAffine Y] :
    P (f ‚â´ g) ‚Üî P f := by rw [‚Üê P.toProperty_apply, ‚Üê P.toProperty_apply, hP.cancel_right_isIso]
#align algebraic_geometry.affine_cancel_right_is_iso AlgebraicGeometry.affine_cancel_right_isIso

theorem affine_arrow_mk_iso_iff {P : AffineTargetMorphismProperty} (hP : P.toProperty.RespectsIso)
    {X Y X' Y' : Scheme} {f : X ‚ü∂ Y} {f' : X' ‚ü∂ Y'}
    (e : Arrow.mk f ‚âÖ Arrow.mk f') {h : IsAffine Y} :
    letI : IsAffine Y' := isAffine_of_isIso (Y := Y) e.inv.right
    P f ‚Üî P f' := by
  rw [‚Üê P.toProperty_apply, ‚Üê P.toProperty_apply, hP.arrow_mk_iso_iff e]

theorem AffineTargetMorphismProperty.respectsIso_mk {P : AffineTargetMorphismProperty}
    (h‚ÇÅ : ‚àÄ {X Y Z} (e : X ‚âÖ Y) (f : Y ‚ü∂ Z) [IsAffine Z], P f ‚Üí P (e.hom ‚â´ f))
    (h‚ÇÇ : ‚àÄ {X Y Z} (e : Y ‚âÖ Z) (f : X ‚ü∂ Y) [h : IsAffine Y],
      P f ‚Üí @P _ _ (f ‚â´ e.hom) (isAffine_of_isIso e.inv)) :
    P.toProperty.RespectsIso := by
  constructor
  ¬∑ rintro X Y Z e f ‚ü®a, h‚ü©; exact ‚ü®a, h‚ÇÅ e f h‚ü©
  ¬∑ rintro X Y Z e f ‚ü®a, h‚ü©; exact ‚ü®isAffine_of_isIso e.inv, h‚ÇÇ e f h‚ü©
#align algebraic_geometry.affine_target_morphism_property.respects_iso_mk AlgebraicGeometry.AffineTargetMorphismProperty.respectsIso_mk

/-- We say that `P : AffineTargetMorphismProperty` is a local property if
1. `P` respects isomorphisms.
2. If `P` holds for `f : X ‚ü∂ Y`, then `P` holds for `f ‚à£_ Y.basicOpen r` for any
  global section `r`.
3. If `P` holds for `f ‚à£_ Y.basicOpen r` for all `r` in a spanning set of the global sections,
  then `P` holds for `f`.
-/
structure AffineTargetMorphismProperty.IsLocal (P : AffineTargetMorphismProperty) : Prop where
  /-- `P` as a morphism property respects isomorphisms -/
  respectsIso : P.toProperty.RespectsIso
  /-- `P` is stable under restriction to basic open set of global sections. -/
  to_basicOpen :
    ‚àÄ {X Y : Scheme} [IsAffine Y] (f : X ‚ü∂ Y) (r : Œì(Y, ‚ä§)),
      P f ‚Üí @P _ _ (f ‚à£_ Y.basicOpen r) ((isAffineOpen_top Y).basicOpen _)
  /-- `P` for `f` if `P` holds for `f` restricted to basic sets of a spanning set of the global
    sections -/
  of_basicOpenCover :
    ‚àÄ {X Y : Scheme} [IsAffine Y] (f : X ‚ü∂ Y) (s : Finset Œì(Y, ‚ä§))
      (_ : Ideal.span (s : Set Œì(Y, ‚ä§)) = ‚ä§),
      (‚àÄ r : s, @P _ _ (f ‚à£_ Y.basicOpen r.1) ((isAffineOpen_top Y).basicOpen _)) ‚Üí P f
#align algebraic_geometry.affine_target_morphism_property.is_local AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal

end AffineTargetMorphismProperty

section targetAffineLocally

/-- For a `P : AffineTargetMorphismProperty`, `targetAffineLocally P` holds for
`f : X ‚ü∂ Y` whenever `P` holds for the restriction of `f` on every affine open subset of `Y`. -/
def targetAffineLocally (P : AffineTargetMorphismProperty) : MorphismProperty Scheme :=
  fun {X Y : Scheme} (f : X ‚ü∂ Y) => ‚àÄ U : Y.affineOpens, P (f ‚à£_ U)
#align algebraic_geometry.target_affine_locally AlgebraicGeometry.targetAffineLocally

variable {P : AffineTargetMorphismProperty} (hP : P.IsLocal) {X Y : Scheme.{u}} (f : X ‚ü∂ Y)

theorem IsAffineOpen.preimage_of_isIso {X Y : Scheme} {U : Opens Y.carrier} (hU : IsAffineOpen U)
    (f : X ‚ü∂ Y) [IsIso f] : IsAffineOpen (f ‚Åª¬π·µÅ U) :=
  haveI : IsAffine _ := hU
  isAffine_of_isIso (f ‚à£_ U)
#align algebraic_geometry.is_affine_open.map_is_iso AlgebraicGeometry.IsAffineOpen.preimage_of_isIso

theorem targetAffineLocally_respectsIso (hP : P.toProperty.RespectsIso) :
    (targetAffineLocally P).RespectsIso := by
  constructor
  ¬∑ introv H U
    rw [morphismRestrict_comp, affine_cancel_left_isIso hP]
    exact H U
  ¬∑ introv H
    rintro ‚ü®U, hU : IsAffineOpen U‚ü©; dsimp
    haveI : IsAffine _ := hU.preimage_of_isIso e.hom
    rw [morphismRestrict_comp, affine_cancel_right_isIso hP]
    exact H ‚ü®(Opens.map e.hom.val.base).obj U, hU.preimage_of_isIso e.hom‚ü©
#align algebraic_geometry.target_affine_locally_respects_iso AlgebraicGeometry.targetAffineLocally_respectsIso

/-- Specialization of `ConcreteCategory.id_apply` because `simp` can't see through the defeq. -/
@[local simp] lemma CommRingCat.id_apply (R : CommRingCat) (x : R) : ùüô R x = x := rfl

attribute [-simp] eqToHom_op in
@[simps!]
def Scheme.resTop (X : Scheme.{u}) (U : Opens X) : Œì(X ‚à£_·µ§ U, ‚ä§) ‚âÖ Œì(X, U) :=
  X.presheaf.mapIso (eqToIso U.openEmbedding_obj_top.symm).op

theorem targetAffineLocally_of_iSup_eq_top
    {Œπ} (U : Œπ ‚Üí Y.affineOpens) (hU : ‚®Ü i, (U i : Opens Y) = ‚ä§) (hU' : ‚àÄ i, P (f ‚à£_ U i)) :
    targetAffineLocally P f := by
  classical
  intro V
  induction V using of_affine_open_cover U hU  with
  | basicOpen U r h =>
    haveI : IsAffine _ := U.2
    have := hP.2 (f ‚à£_ U.1)
    replace this := this ((Y.resTop U).inv r) h
    rw [‚Üê P.toProperty_apply] at this ‚ä¢
    exact (hP.1.arrow_mk_iso_iff (morphismRestrictRestrictBasicOpen f _ r)).mp this
  | openCover U s hs H =>
    apply hP.3 _ (s.image (Y.resTop _).inv) (by simp [‚Üê Ideal.map_span, hs, Ideal.map_top])
    intro ‚ü®r, hr‚ü©
    obtain ‚ü®r, hr', rfl‚ü© := Finset.mem_image.mp hr
    exact (affine_arrow_mk_iso_iff hP.1
      (morphismRestrictRestrictBasicOpen f _ r).symm).mp (H ‚ü®r, hr'‚ü©)
  | hU i => exact hU' i

theorem targetAffineLocally_iff_of_iSup_eq_top
    {Œπ} (U : Œπ ‚Üí Y.affineOpens) (hU : ‚®Ü i, (U i : Opens Y) = ‚ä§) :
    targetAffineLocally P f ‚Üî ‚àÄ i, P (f ‚à£_ U i) :=
  ‚ü®fun H _ ‚Ü¶ H _, fun H ‚Ü¶ targetAffineLocally_of_iSup_eq_top hP f U hU H‚ü©

theorem targetAffineLocally_of_openCover
    (ùí∞ : Y.OpenCover) [‚àÄ i, IsAffine (ùí∞.obj i)] (hùí∞ : ‚àÄ i, P (ùí∞.pullbackHom f i)) :
    targetAffineLocally P f := by
  apply targetAffineLocally_of_iSup_eq_top hP f
    (fun i ‚Ü¶ ‚ü®_, isAffineOpen_opensRange (ùí∞.map i)‚ü©) ùí∞.iSup_opensRange
    (fun i ‚Ü¶ (affine_arrow_mk_iso_iff hP.1 (morphismRestrictOpensRange f _)).mpr (hùí∞ i))
#align algebraic_geometry.target_affine_locally_of_open_cover AlgebraicGeometry.targetAffineLocally_of_openCover

theorem targetAffineLocally_iff_of_openCover
    (ùí∞ : Y.OpenCover) [‚àÄ i, IsAffine (ùí∞.obj i)] :
    targetAffineLocally P f ‚Üî ‚àÄ i, P (ùí∞.pullbackHom f i) := by
  rw [targetAffineLocally_iff_of_iSup_eq_top hP f
    (fun i ‚Ü¶ ‚ü®_, isAffineOpen_opensRange _‚ü©) ùí∞.iSup_opensRange]
  exact forall_congr' fun i ‚Ü¶ affine_arrow_mk_iso_iff hP.1 (morphismRestrictOpensRange f _)

theorem targetAffineLocally_morphsimRestrict
    (U : Opens Y) (H : targetAffineLocally P f) :
    targetAffineLocally P (f ‚à£_ U) := by
  intros V
  rw [affine_arrow_mk_iso_iff hP.respectsIso (morphismRestrictRestrict _ _ _)]
  exact H ‚ü®_, V.2.image_of_isOpenImmersion _‚ü©

theorem targetAffineLocally_pullback {U} (g : U ‚ü∂ Y) [IsOpenImmersion g]
    (h : targetAffineLocally P f) :
    targetAffineLocally P (pullback.snd : pullback f g ‚ü∂ _) := by
  rw [‚Üê (targetAffineLocally_respectsIso hP.1).arrow_mk_iso_iff (morphismRestrictOpensRange _ _)]
  exact targetAffineLocally_morphsimRestrict hP f _ h

theorem AffineTargetMorphismProperty.isLocalOfOpenCoverImply (P : AffineTargetMorphismProperty)
    (hP : P.toProperty.RespectsIso)
    (H : ‚àÄ {X Y : Scheme.{u}} (f : X ‚ü∂ Y),
      (‚àÉ (ùí∞ : Scheme.OpenCover.{u} Y) (_ : ‚àÄ i, IsAffine (ùí∞.obj i)),
        ‚àÄ i : ùí∞.J, P (pullback.snd : (ùí∞.pullbackCover f).obj i ‚ü∂ ùí∞.obj i)) ‚Üí
        ‚àÄ {U : Scheme} (g : U ‚ü∂ Y) [IsAffine U] [IsOpenImmersion g],
          P (pullback.snd : pullback f g ‚ü∂ U)) :
    P.IsLocal := by
  refine ‚ü®hP, ?_, ?_‚ü©
  ¬∑ introv h
    haveI : IsAffine _ := (isAffineOpen_top Y).basicOpen r
    delta morphismRestrict
    rw [affine_cancel_left_isIso hP]
    refine @H _ _ f ‚ü®Scheme.openCoverOfIsIso (ùüô Y), ?_, ?_‚ü© _ (Y.ofRestrict _) _ _
    ¬∑ intro i; dsimp; infer_instance
    ¬∑ intro i; dsimp
      rwa [‚Üê Category.comp_id pullback.snd, ‚Üê pullback.condition, affine_cancel_left_isIso hP]
  ¬∑ introv hs hs'
    replace hs := ((isAffineOpen_top Y).basicOpen_union_eq_self_iff _).mpr hs
    have := H f ‚ü®Y.openCoverOfSuprEqTop _ hs, ?_, ?_‚ü© (ùüô _)
    ¬∑ rwa [‚Üê Category.comp_id pullback.snd, ‚Üê pullback.condition, affine_cancel_left_isIso hP]
        at this
    ¬∑ intro i; exact (isAffineOpen_top Y).basicOpen _
    ¬∑ rintro (i : s)
      specialize hs' i
      haveI : IsAffine _ := (isAffineOpen_top Y).basicOpen i.1
      delta morphismRestrict at hs'
      rwa [affine_cancel_left_isIso hP] at hs'
#align algebraic_geometry.affine_target_morphism_property.is_local_of_open_cover_imply AlgebraicGeometry.AffineTargetMorphismProperty.isLocalOfOpenCoverImply

theorem targetAffineLocally_iff_of_isAffine [IsAffine Y] :
    targetAffineLocally P f ‚Üî P f := by
  haveI : ‚àÄ i, IsAffine (Scheme.OpenCover.obj (Scheme.openCoverOfIsIso (ùüô Y)) i) := fun i => by
    dsimp; infer_instance
  rw [targetAffineLocally_iff_of_openCover hP f (Scheme.openCoverOfIsIso.{0} (ùüô Y))]
  trans P (pullback.snd : pullback f (ùüô _) ‚ü∂ _)
  ¬∑ exact ‚ü®fun H => H PUnit.unit, fun H _ => H‚ü©
  rw [‚Üê Category.comp_id pullback.snd, ‚Üê pullback.condition, affine_cancel_left_isIso hP.1]
#align algebraic_geometry.affine_target_morphism_property.is_local.affine_target_iff AlgebraicGeometry.targetAffineLocally_iff_of_isAffine

theorem targetAffineLocally_morphsimRestrict_affineOpens
    (U : Y.affineOpens) (H : targetAffineLocally P f) : P (f ‚à£_ U) := by
  rw [‚Üê targetAffineLocally_iff_of_isAffine hP]
  exact targetAffineLocally_morphsimRestrict hP f _ H

theorem targetAffineLocally_pullback_of_isAffine {U} (g : U ‚ü∂ Y) [IsOpenImmersion g] [IsAffine U]
    (h : targetAffineLocally P f) : P (pullback.snd : pullback f g ‚ü∂ _) := by
  rw [‚Üê targetAffineLocally_iff_of_isAffine hP]
  exact targetAffineLocally_pullback hP f _ h

instance {X : Scheme} (ùí∞ : X.AffineOpenCover) (i) : IsAffine (ùí∞.openCover.obj i) := by
  dsimp; infer_instance

def Scheme.OpenCover.pullbackCoverAffineRefinementObj (ùí∞ : Y.OpenCover) (i) :
    (ùí∞.affineRefinement.openCover.pullbackCover f).obj i ‚âÖ
      pullback (ùí∞.pullbackHom f i.1) ((ùí∞.obj i.1).affineCover.map i.2) := by
  refine pullbackSymmetry _ _ ‚â™‚â´ (pullbackRightPullbackFstIso _ _ _).symm ‚â™‚â´
    pullbackSymmetry _ _ ‚â™‚â´
      asIso (pullback.map _ _ _ _ (pullbackSymmetry _ _).hom (ùüô _) (ùüô _) ?_ ?_)
  ¬∑ simp [pullbackHom]
  ¬∑ simp

@[reassoc (attr := simp)]
lemma Scheme.OpenCover.pullbackCoverAffineRefinementObj_inv_map (ùí∞ : Y.OpenCover) (i) :
    (ùí∞.pullbackCoverAffineRefinementObj f i).inv ‚â´
      (ùí∞.affineRefinement.openCover.pullbackCover f).map i =
      pullback.fst ‚â´ (ùí∞.pullbackCover f).map i.1 := by
  simp only [pullbackCover_obj, AffineOpenCover.openCover_obj, AffineOpenCover.openCover_map,
    pullbackCoverAffineRefinementObj, Iso.trans_inv, asIso_inv, Iso.symm_inv, Category.assoc,
    pullbackCover_map, pullbackSymmetry_inv_comp_fst, IsIso.inv_comp_eq, limit.lift_œÄ_assoc, id_eq,
    PullbackCone.mk_pt, cospan_left, PullbackCone.mk_œÄ_app, pullbackSymmetry_hom_comp_fst]
  erw [pullbackRightPullbackFstIso_hom_snd]
  simp only [pullbackSymmetry_inv_comp_snd_assoc]

@[reassoc (attr := simp)]
lemma Scheme.OpenCover.pullbackCoverAffineRefinementObj_inv_pullbackHom (ùí∞ : Y.OpenCover) (i) :
    (ùí∞.pullbackCoverAffineRefinementObj f i).inv ‚â´
      ùí∞.affineRefinement.openCover.pullbackHom f i =
      pullback.snd := by
  simp only [pullbackCover_obj, pullbackHom, AffineOpenCover.openCover_obj,
    AffineOpenCover.openCover_map, pullbackCoverAffineRefinementObj, Iso.trans_inv, asIso_inv,
    Iso.symm_inv, Category.assoc, pullbackSymmetry_inv_comp_snd, IsIso.inv_comp_eq, limit.lift_œÄ,
    id_eq, PullbackCone.mk_pt, PullbackCone.mk_œÄ_app, Category.comp_id]
  erw [pullbackRightPullbackFstIso_hom_fst]
  simp only [pullbackSymmetry_inv_comp_fst]

theorem targetAffineLocally_isLocal
    {P : AffineTargetMorphismProperty} (hP : P.IsLocal) :
    PropertyIsLocalAtTarget (targetAffineLocally P) := by
  constructor
  ¬∑ exact targetAffineLocally_respectsIso hP.1
  ¬∑ intro X Y f U H V
    rw [‚Üê P.toProperty_apply (i := V.2), hP.1.arrow_mk_iso_iff (morphismRestrictRestrict f _ _)]
    convert H ‚ü®_, V.2.image_of_isOpenImmersion (Y.ofRestrict _)‚ü©
    rw [‚Üê P.toProperty_apply]
  ¬∑ rintro X Y f Us hU H
    let ùí∞ := Y.openCoverOfSuprEqTop (fun i : Us ‚Ü¶ i) (by rwa [iSup_subtype, ‚Üê sSup_eq_iSup])
    apply targetAffineLocally_of_openCover hP _ ùí∞.affineRefinement.openCover
    rintro ‚ü®i, j‚ü©
    have : targetAffineLocally P (ùí∞.pullbackHom f i) := by
      refine ((targetAffineLocally_respectsIso hP.1).arrow_mk_iso_iff
        (morphismRestrictEq _ ?_ ‚â™‚â´ morphismRestrictOpensRange f (ùí∞.map i))).mp (H _ i.2)
      exact (opensRange_ŒπOpens _).symm
    rw [‚Üê affine_cancel_left_isIso hP.1 (ùí∞.pullbackCoverAffineRefinementObj f _).inv,
      ùí∞.pullbackCoverAffineRefinementObj_inv_pullbackHom]
    exact targetAffineLocally_pullback_of_isAffine hP _ ((ùí∞.obj i).affineCover.map j) this
#align algebraic_geometry.affine_target_morphism_property.is_local.target_affine_locally_is_local AlgebraicGeometry.targetAffineLocally_isLocal

end targetAffineLocally

namespace AffineTargetMorphismProperty

/-- A `P : AffineTargetMorphismProperty` is stable under base change if `P` holds for `Y ‚ü∂ S`
implies that `P` holds for `X √ó‚Çõ Y ‚ü∂ X` with `X` and `S` affine schemes. -/
def StableUnderBaseChange (P : AffineTargetMorphismProperty) : Prop :=
  ‚àÄ ‚¶ÉX Y S : Scheme‚¶Ñ [IsAffine S] [IsAffine X] (f : X ‚ü∂ S) (g : Y ‚ü∂ S),
    P g ‚Üí P (pullback.fst : pullback f g ‚ü∂ X)
#align algebraic_geometry.affine_target_morphism_property.stable_under_base_change AlgebraicGeometry.AffineTargetMorphismProperty.StableUnderBaseChange

theorem IsLocal.targetAffineLocally_pullback_fst_of_right_of_stableUnderBaseChange
    {P : AffineTargetMorphismProperty} (hP : P.IsLocal) (hP' : P.StableUnderBaseChange)
    {X Y S : Scheme} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [IsAffine S] (H : P g) :
    targetAffineLocally P (pullback.fst : pullback f g ‚ü∂ X) := by
  rw [targetAffineLocally_iff_of_openCover hP _ X.affineCover]
  intro i
  let e := pullbackSymmetry _ _ ‚â™‚â´ pullbackRightPullbackFstIso f g (X.affineCover.map i)
  have : e.hom ‚â´ pullback.fst = X.affineCover.pullbackHom pullback.fst i := by
    simp [e, Scheme.OpenCover.pullbackHom]
  rw [‚Üê this, affine_cancel_left_isIso hP.1]
  apply hP'; assumption
#align algebraic_geometry.affine_target_morphism_property.is_local.target_affine_locally_pullback_fst_of_right_of_stable_under_base_change AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal.targetAffineLocally_pullback_fst_of_right_of_stableUnderBaseChange

theorem IsLocal.stableUnderBaseChange {P : AffineTargetMorphismProperty} (hP : P.IsLocal)
    (hP' : P.StableUnderBaseChange) : (targetAffineLocally P).StableUnderBaseChange :=
  MorphismProperty.StableUnderBaseChange.mk (targetAffineLocally_respectsIso hP.respectsIso)
    (fun X Y S f g H => by
      rw [(targetAffineLocally_isLocal hP).iff_of_openCover _ (S.affineCover.pullbackCover f)]
      intro i
      let e : pullback (pullback.fst : pullback f g ‚ü∂ _) ((S.affineCover.pullbackCover f).map i) ‚âÖ
          _ := by
        refine pullbackSymmetry _ _ ‚â™‚â´ pullbackRightPullbackFstIso f g _ ‚â™‚â´ ?_ ‚â™‚â´
          (pullbackRightPullbackFstIso (S.affineCover.map i) g
            (pullback.snd : pullback f (S.affineCover.map i) ‚ü∂ _)).symm
        exact asIso
          (pullback.map _ _ _ _ (ùüô _) (ùüô _) (ùüô _) (by simpa using pullback.condition) (by simp))
      have : e.hom ‚â´ pullback.fst =
          (S.affineCover.pullbackCover f).pullbackHom pullback.fst i := by
        simp [e, Scheme.OpenCover.pullbackHom]
      rw [‚Üê this, (targetAffineLocally_respectsIso hP.1).cancel_left_isIso]
      apply hP.targetAffineLocally_pullback_fst_of_right_of_stableUnderBaseChange hP'
      rw [‚Üê pullbackSymmetry_hom_comp_snd, affine_cancel_left_isIso hP.1]
      apply targetAffineLocally_pullback_of_isAffine hP _ _ H)
#align algebraic_geometry.affine_target_morphism_property.is_local.stable_under_base_change AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal.stableUnderBaseChange

end AffineTargetMorphismProperty

/-- The `AffineTargetMorphismProperty` associated to `(targetAffineLocally P).diagonal`.
See `diagonal_targetAffineLocally_eq_targetAffineLocally`.
-/
def AffineTargetMorphismProperty.diagonal (P : AffineTargetMorphismProperty) :
    AffineTargetMorphismProperty :=
  fun {X _} f _ =>
    ‚àÄ {U‚ÇÅ U‚ÇÇ : Scheme} (f‚ÇÅ : U‚ÇÅ ‚ü∂ X) (f‚ÇÇ : U‚ÇÇ ‚ü∂ X) [IsAffine U‚ÇÅ] [IsAffine U‚ÇÇ] [IsOpenImmersion f‚ÇÅ]
      [IsOpenImmersion f‚ÇÇ], P (pullback.mapDesc f‚ÇÅ f‚ÇÇ f)
#align algebraic_geometry.affine_target_morphism_property.diagonal AlgebraicGeometry.AffineTargetMorphismProperty.diagonal

theorem AffineTargetMorphismProperty.diagonal_respectsIso (P : AffineTargetMorphismProperty)
    (hP : P.toProperty.RespectsIso) : P.diagonal.toProperty.RespectsIso := by
  delta AffineTargetMorphismProperty.diagonal
  apply AffineTargetMorphismProperty.respectsIso_mk
  ¬∑ introv H _ _
    rw [pullback.mapDesc_comp, affine_cancel_left_isIso hP, affine_cancel_right_isIso hP]
    apply H
  ¬∑ introv H _ _
    rw [pullback.mapDesc_comp, affine_cancel_right_isIso hP]
    apply H
#align algebraic_geometry.affine_target_morphism_property.diagonal_respects_iso AlgebraicGeometry.AffineTargetMorphismProperty.diagonal_respectsIso

theorem diagonal_targetAffineLocally_of_openCover
    (P : AffineTargetMorphismProperty) (hP : P.IsLocal)
    {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (ùí∞ : Scheme.OpenCover.{u} Y) [‚àÄ i, IsAffine (ùí∞.obj i)]
    (ùí∞' : ‚àÄ i, Scheme.OpenCover.{u} (pullback f (ùí∞.map i))) [‚àÄ i j, IsAffine ((ùí∞' i).obj j)]
    (hùí∞' : ‚àÄ i j k, P (pullback.mapDesc ((ùí∞' i).map j) ((ùí∞' i).map k) pullback.snd)) :
    (targetAffineLocally P).diagonal f := by
  let ùí± := (Scheme.Pullback.openCoverOfBase ùí∞ f f).bind fun i =>
    Scheme.Pullback.openCoverOfLeftRight.{u} (ùí∞' i) (ùí∞' i) pullback.snd pullback.snd
  have i1 : ‚àÄ i, IsAffine (ùí±.obj i) := fun i => by dsimp [ùí±]; infer_instance
  apply (hP.affine_openCover_iff _ ùí±).mpr
  rintro ‚ü®i, j, k‚ü©
  dsimp [ùí±]
  convert (affine_cancel_left_isIso hP.1
    (pullbackDiagonalMapIso _ _ ((ùí∞' i).map j) ((ùí∞' i).map k)).inv pullback.snd).mp _
  pick_goal 3
  ¬∑ convert hùí∞' i j k; apply pullback.hom_ext <;> simp
  all_goals apply pullback.hom_ext <;>
  simp only [Category.assoc, pullback.lift_fst, pullback.lift_snd, pullback.lift_fst_assoc,
    pullback.lift_snd_assoc]
#align algebraic_geometry.diagonal_target_affine_locally_of_open_cover AlgebraicGeometry.diagonal_targetAffineLocally_of_openCover

theorem AffineTargetMorphismProperty.diagonal_of_targetAffineLocally
    (P : AffineTargetMorphismProperty) (hP : P.IsLocal) {X Y U : Scheme.{u}} (f : X ‚ü∂ Y) (g : U ‚ü∂ Y)
    [IsAffine U] [IsOpenImmersion g] (H : (targetAffineLocally P).diagonal f) :
    P.diagonal (pullback.snd : pullback f g ‚ü∂ _) := by
  rintro U V f‚ÇÅ f‚ÇÇ hU hV hf‚ÇÅ hf‚ÇÇ
  replace H := ((hP.affine_openCover_TFAE (pullback.diagonal f)).out 0 3).mp H
  let g‚ÇÅ := pullback.map (f‚ÇÅ ‚â´ pullback.snd) (f‚ÇÇ ‚â´ pullback.snd) f f
    (f‚ÇÅ ‚â´ pullback.fst) (f‚ÇÇ ‚â´ pullback.fst) g
    (by rw [Category.assoc, Category.assoc, pullback.condition])
    (by rw [Category.assoc, Category.assoc, pullback.condition])
  specialize H g‚ÇÅ
  rw [‚Üê affine_cancel_left_isIso hP.1 (pullbackDiagonalMapIso f _ f‚ÇÅ f‚ÇÇ).hom]
  convert H
  apply pullback.hom_ext <;>
    simp only [Category.assoc, pullback.lift_fst, pullback.lift_snd, pullback.lift_fst_assoc,
      pullback.lift_snd_assoc, Category.comp_id, pullbackDiagonalMapIso_hom_fst,
      pullbackDiagonalMapIso_hom_snd]
#align algebraic_geometry.affine_target_morphism_property.diagonal_of_target_affine_locally AlgebraicGeometry.AffineTargetMorphismProperty.diagonal_of_targetAffineLocally

open List in
theorem AffineTargetMorphismProperty.IsLocal.diagonal_affine_openCover_TFAE
    {P : AffineTargetMorphismProperty} (hP : P.IsLocal) {X Y : Scheme.{u}} (f : X ‚ü∂ Y) :
    TFAE
      [(targetAffineLocally P).diagonal f,
        ‚àÉ (ùí∞ : Scheme.OpenCover.{u} Y) (_ : ‚àÄ i, IsAffine (ùí∞.obj i)),
          ‚àÄ i : ùí∞.J, P.diagonal (pullback.snd : pullback f (ùí∞.map i) ‚ü∂ _),
        ‚àÄ (ùí∞ : Scheme.OpenCover.{u} Y) [‚àÄ i, IsAffine (ùí∞.obj i)] (i : ùí∞.J),
          P.diagonal (pullback.snd : pullback f (ùí∞.map i) ‚ü∂ _),
        ‚àÄ {U : Scheme} (g : U ‚ü∂ Y) [IsAffine U] [IsOpenImmersion g],
          P.diagonal (pullback.snd : pullback f g ‚ü∂ _),
        ‚àÉ (ùí∞ : Scheme.OpenCover.{u} Y) (_ : ‚àÄ i, IsAffine (ùí∞.obj i)) (ùí∞' :
          ‚àÄ i, Scheme.OpenCover.{u} (pullback f (ùí∞.map i))) (_ : ‚àÄ i j, IsAffine ((ùí∞' i).obj j)),
          ‚àÄ i j k, P (pullback.mapDesc ((ùí∞' i).map j) ((ùí∞' i).map k) pullback.snd)] := by
  tfae_have 1 ‚Üí 4
  ¬∑ introv H hU hg _ _; apply P.diagonal_of_targetAffineLocally <;> assumption
  tfae_have 4 ‚Üí 3
  ¬∑ introv H hùí∞; apply H
  tfae_have 3 ‚Üí 2
  ¬∑ exact fun H => ‚ü®Y.affineCover, inferInstance, H Y.affineCover‚ü©
  tfae_have 2 ‚Üí 5
  ¬∑ rintro ‚ü®ùí∞, hùí∞, H‚ü©
    refine ‚ü®ùí∞, inferInstance, fun _ => Scheme.affineCover _, inferInstance, ?_‚ü©
    intro i j k
    apply H
  tfae_have 5 ‚Üí 1
  ¬∑ rintro ‚ü®ùí∞, _, ùí∞', _, H‚ü©
    exact diagonal_targetAffineLocally_of_openCover P hP f ùí∞ ùí∞' H
  tfae_finish
#align algebraic_geometry.affine_target_morphism_property.is_local.diagonal_affine_open_cover_tfae AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal.diagonal_affine_openCover_TFAE

theorem AffineTargetMorphismProperty.IsLocal.diagonal {P : AffineTargetMorphismProperty}
    (hP : P.IsLocal) : P.diagonal.IsLocal := by
  constructor
  ¬∑ exact hP.respectsIso.diagonal
  -- AffineTargetMorphismProperty.isLocalOfOpenCoverImply P.diagonal (P.diagonal_respectsIso hP.1)
  --   fun {_ _} f => ((hP.diagonal_affine_openCover_TFAE f).out 1 3).mp
#align algebraic_geometry.affine_target_morphism_property.is_local.diagonal AlgebraicGeometry.AffineTargetMorphismProperty.IsLocal.diagonal

theorem diagonal_targetAffineLocally_eq_targetAffineLocally (P : AffineTargetMorphismProperty)
    (hP : P.IsLocal) : (targetAffineLocally P).diagonal = targetAffineLocally P.diagonal := by
  ext _ _ f
  exact ((hP.diagonal_affine_openCover_TFAE f).out 0 1).trans
    ((hP.diagonal.affine_openCover_TFAE f).out 1 0)
#align algebraic_geometry.diagonal_target_affine_locally_eq_target_affine_locally AlgebraicGeometry.diagonal_targetAffineLocally_eq_targetAffineLocally

theorem universally_isLocalAtTarget (P : MorphismProperty Scheme)
    (hP : ‚àÄ {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (ùí∞ : Scheme.OpenCover.{u} Y),
      (‚àÄ i : ùí∞.J, P (pullback.snd : (ùí∞.pullbackCover f).obj i ‚ü∂ ùí∞.obj i)) ‚Üí P f) :
    PropertyIsLocalAtTarget P.universally := by
  refine ‚ü®P.universally_respectsIso, fun {X Y} f U =>
    P.universally_stableUnderBaseChange (isPullback_morphismRestrict f U).flip, ?_‚ü©
  intro X Y f ùí∞ h X' Y' i‚ÇÅ i‚ÇÇ f' H
  apply hP _ (ùí∞.pullbackCover i‚ÇÇ)
  intro i
  dsimp
  apply h i (pullback.lift (pullback.fst ‚â´ i‚ÇÅ) (pullback.snd ‚â´ pullback.snd) _) pullback.snd
  swap
  ¬∑ rw [Category.assoc, Category.assoc, ‚Üê pullback.condition, ‚Üê pullback.condition_assoc, H.w]
  refine (IsPullback.of_right ?_ (pullback.lift_snd _ _ _) (IsPullback.of_hasPullback _ _)).flip
  rw [pullback.lift_fst, ‚Üê pullback.condition]
  exact (IsPullback.of_hasPullback _ _).paste_horiz H.flip
#align algebraic_geometry.universally_is_local_at_target AlgebraicGeometry.universally_isLocalAtTarget

theorem universally_isLocalAtTarget_of_morphismRestrict (P : MorphismProperty Scheme)
    (hP‚ÇÅ : P.RespectsIso)
    (hP‚ÇÇ : ‚àÄ {X Y : Scheme.{u}} (f : X ‚ü∂ Y) {Œπ : Type u} (U : Œπ ‚Üí Opens Y.carrier)
      (_ : iSup U = ‚ä§), (‚àÄ i, P (f ‚à£_ U i)) ‚Üí P f) : PropertyIsLocalAtTarget P.universally :=
  universally_isLocalAtTarget P (fun f ùí∞ hùí∞ => by
    apply hP‚ÇÇ f (fun i : ùí∞.J => Scheme.Hom.opensRange (ùí∞.map i)) ùí∞.iSup_opensRange
    simp_rw [hP‚ÇÅ.arrow_mk_iso_iff (morphismRestrictOpensRange f _)]
    exact hùí∞)
#align algebraic_geometry.universally_is_local_at_target_of_morphism_restrict AlgebraicGeometry.universally_isLocalAtTarget_of_morphismRestrict

theorem morphismRestrict_base {X Y : Scheme} (f : X ‚ü∂ Y) (U : Opens Y.carrier) :
    ‚áë(f ‚à£_ U).1.base = U.1.restrictPreimage f.1.1 :=
  funext fun x => Subtype.ext <| morphismRestrict_base_coe f U x
#align algebraic_geometry.morphism_restrict_base AlgebraicGeometry.morphismRestrict_base

/-- `topologically P` holds for a morphism if the underlying topological map satisfies `P`. -/
def MorphismProperty.topologically
    (P : ‚àÄ {Œ± Œ≤ : Type u} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] (_ : Œ± ‚Üí Œ≤), Prop) :
    MorphismProperty Scheme.{u} := fun _ _ f => P f.1.base
#align algebraic_geometry.morphism_property.topologically AlgebraicGeometry.MorphismProperty.topologically

variable (P : ‚àÄ {Œ± Œ≤ : Type u} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] (_ : Œ± ‚Üí Œ≤), Prop)

/-- If a property of maps of topological spaces is stable under composition, the induced
morphism property of schemes is stable under composition. -/
lemma MorphismProperty.topologically_isStableUnderComposition
    (hP : ‚àÄ {Œ± Œ≤ Œ≥ : Type u} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]
      (f : Œ± ‚Üí Œ≤) (g : Œ≤ ‚Üí Œ≥) (_ : P f) (_ : P g), P (g ‚àò f)) :
    (MorphismProperty.topologically P).IsStableUnderComposition where
  comp_mem {X Y Z} f g hf hg := by
    simp only [MorphismProperty.topologically, Scheme.comp_coeBase, TopCat.coe_comp]
    exact hP _ _ hf hg

/-- If a property of maps of topological spaces is satisfied by all homeomorphisms,
every isomorphism of schemes satisfies the induced property. -/
lemma MorphismProperty.topologically_iso_le
    (hP : ‚àÄ {Œ± Œ≤ : Type u} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] (f : Œ± ‚âÉ‚Çú Œ≤), P f) :
    MorphismProperty.isomorphisms Scheme ‚â§ (MorphismProperty.topologically P) := by
  intro X Y e (he : IsIso e)
  have : IsIso e := he
  exact hP (TopCat.homeoOfIso (asIso e.val.base))

/-- If a property of maps of topological spaces is satisfied by homeomorphisms and is stable
under composition, the induced property on schemes respects isomorphisms. -/
lemma MorphismProperty.topologically_respectsIso
    (hP‚ÇÅ : ‚àÄ {Œ± Œ≤ : Type u} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] (f : Œ± ‚âÉ‚Çú Œ≤), P f)
    (hP‚ÇÇ : ‚àÄ {Œ± Œ≤ Œ≥ : Type u} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]
      (f : Œ± ‚Üí Œ≤) (g : Œ≤ ‚Üí Œ≥) (_ : P f) (_ : P g), P (g ‚àò f)) :
      (MorphismProperty.topologically P).RespectsIso :=
  have : (MorphismProperty.topologically P).IsStableUnderComposition :=
    topologically_isStableUnderComposition P hP‚ÇÇ
  MorphismProperty.respectsIso_of_isStableUnderComposition (topologically_iso_le P hP‚ÇÅ)

/-- To check that a topologically defined morphism property is local at the target,
we may check the corresponding properties on topological spaces. -/
lemma MorphismProperty.topologically_propertyIsLocalAtTarget
    (hP‚ÇÅ : (MorphismProperty.topologically P).RespectsIso)
    (hP‚ÇÇ : ‚àÄ {Œ± Œ≤ : Type u} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] (f : Œ± ‚Üí Œ≤) (s : Set Œ≤),
      P f ‚Üí P (s.restrictPreimage f))
    (hP‚ÇÉ : ‚àÄ {Œ± Œ≤ : Type u} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] (f : Œ± ‚Üí Œ≤) {Œπ : Type u}
      (U : Œπ ‚Üí TopologicalSpace.Opens Œ≤) (_ : iSup U = ‚ä§) (_ : Continuous f),
      (‚àÄ i, P ((U i).carrier.restrictPreimage f)) ‚Üí P f) :
    PropertyIsLocalAtTarget (MorphismProperty.topologically P) := by
  apply propertyIsLocalAtTarget_of_morphismRestrict
  ¬∑ exact hP‚ÇÅ
  ¬∑ intro X Y f U hf
    simp_rw [MorphismProperty.topologically, morphismRestrict_base]
    exact hP‚ÇÇ f.val.base U.carrier hf
  ¬∑ intro X Y f Œπ U hU hf
    apply hP‚ÇÉ f.val.base U hU
    ¬∑ exact f.val.base.continuous
    ¬∑ intro i
      rw [‚Üê morphismRestrict_base]
      exact hf i

namespace AffineTargetMorphismProperty.IsLocal

@[deprecated (since := "2024-06-22")]
alias targetAffineLocallyIsLocal := targetAffineLocally_isLocal

@[deprecated (since := "2024-06-22")]
alias targetAffineLocallyPullbackFstOfRightOfStableUnderBaseChange :=
  targetAffineLocally_pullback_fst_of_right_of_stableUnderBaseChange

end AffineTargetMorphismProperty.IsLocal

@[deprecated (since := "2024-06-22")]
alias diagonalTargetAffineLocallyOfOpenCover := diagonal_targetAffineLocally_of_openCover

@[deprecated (since := "2024-06-22")]
alias AffineTargetMorphismProperty.diagonalOfTargetAffineLocally :=
  AffineTargetMorphismProperty.diagonal_of_targetAffineLocally

@[deprecated (since := "2024-06-22")]
alias universallyIsLocalAtTarget := universally_isLocalAtTarget

@[deprecated (since := "2024-06-22")]
alias universallyIsLocalAtTargetOfMorphismRestrict :=
  universally_isLocalAtTarget_of_morphismRestrict

end AlgebraicGeometry
