/-
Copyright (c) 2024 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.RingHomProperties
import Mathlib.RingTheory.RingHom.Surjective
import Mathlib.RingTheory.Spectrum.Prime.TensorProduct
import Mathlib.Topology.LocalAtTarget

/-!
# Morphisms surjective on stalks

We define the class of morphisms between schemes that are surjective on stalks.
We show that this class is stable under composition and base change.

We also show that (`AlgebraicGeometry.SurjectiveOnStalks.isEmbedding_pullback`)
if `Y âŸ¶ S` is surjective on stalks, then for every `X âŸ¶ S`, `X Ã—â‚› Y` is a subset of
`X Ã— Y` (cartesian product as topological spaces) with the induced topology.
-/

open CategoryTheory CategoryTheory.Limits Topology

namespace AlgebraicGeometry

universe u

variable {X Y Z : Scheme.{u}} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)

/-- The class of morphisms `f : X âŸ¶ Y` between schemes such that
`ğ’ª_{Y, f x} âŸ¶ ğ’ª_{X, x}` is surjective for all `x : X`. -/
@[mk_iff]
class SurjectiveOnStalks : Prop where
  surj_on_stalks : âˆ€ x, Function.Surjective (f.stalkMap x)

theorem Scheme.Hom.stalkMap_surjective (f : X.Hom Y) [SurjectiveOnStalks f] (x) :
    Function.Surjective (f.stalkMap x) :=
  SurjectiveOnStalks.surj_on_stalks x

namespace SurjectiveOnStalks

instance (priority := 900) [IsOpenImmersion f] : SurjectiveOnStalks f :=
  âŸ¨fun _ â†¦ (ConcreteCategory.bijective_of_isIso _).2âŸ©

instance : MorphismProperty.IsMultiplicative @SurjectiveOnStalks where
  id_mem _ := inferInstance
  comp_mem {X Y Z} f g hf hg := by
    refine âŸ¨fun x â†¦ ?_âŸ©
    rw [Scheme.stalkMap_comp]
    exact (hf.surj_on_stalks x).comp (hg.surj_on_stalks (f.base x))

instance comp {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [SurjectiveOnStalks f]
    [SurjectiveOnStalks g] : SurjectiveOnStalks (f â‰« g) :=
  MorphismProperty.IsStableUnderComposition.comp_mem f g inferInstance inferInstance

lemma eq_stalkwise :
    @SurjectiveOnStalks = stalkwise (Function.Surjective Â·) := by
  ext; exact surjectiveOnStalks_iff _

instance : IsLocalAtTarget @SurjectiveOnStalks :=
  eq_stalkwise â–¸ stalkwiseIsLocalAtTarget_of_respectsIso RingHom.surjective_respectsIso

instance : IsLocalAtSource @SurjectiveOnStalks :=
  eq_stalkwise â–¸ stalkwise_isLocalAtSource_of_respectsIso RingHom.surjective_respectsIso

lemma Spec_iff {R S : CommRingCat.{u}} {Ï† : R âŸ¶ S} :
    SurjectiveOnStalks (Spec.map Ï†) â†” RingHom.SurjectiveOnStalks Ï†.hom := by
  rw [eq_stalkwise, stalkwise_Spec_map_iff RingHom.surjective_respectsIso,
    RingHom.SurjectiveOnStalks]

instance : HasRingHomProperty @SurjectiveOnStalks RingHom.SurjectiveOnStalks :=
  eq_stalkwise â–¸ .stalkwise RingHom.surjective_respectsIso

variable {f} in
lemma iff_of_isAffine [IsAffine X] [IsAffine Y] :
    SurjectiveOnStalks f â†” RingHom.SurjectiveOnStalks (f.app âŠ¤).hom := by
  rw [â† Spec_iff, MorphismProperty.arrow_mk_iso_iff @SurjectiveOnStalks (arrowIsoSpecÎ“OfIsAffine f)]

theorem of_comp [SurjectiveOnStalks (f â‰« g)] : SurjectiveOnStalks f := by
  refine âŸ¨fun x â†¦ ?_âŸ©
  have := (f â‰« g).stalkMap_surjective x
  rw [Scheme.stalkMap_comp] at this
  exact Function.Surjective.of_comp this

instance stableUnderBaseChange :
    MorphismProperty.IsStableUnderBaseChange @SurjectiveOnStalks := by
  apply HasRingHomProperty.isStableUnderBaseChange
  apply RingHom.IsStableUnderBaseChange.mk
  Â· exact (HasRingHomProperty.isLocal_ringHomProperty @SurjectiveOnStalks).respectsIso
  intros R S T _ _ _ _ _ H
  exact H.baseChange

variable {f} in
lemma mono_of_injective [SurjectiveOnStalks f] (hf : Function.Injective f.base) : Mono f := by
  refine (Scheme.forgetToLocallyRingedSpace â‹™
    LocallyRingedSpace.forgetToSheafedSpace).mono_of_mono_map ?_
  apply SheafedSpace.mono_of_base_injective_of_stalk_epi
  Â· exact hf
  Â· exact fun x â†¦ ConcreteCategory.epi_of_surjective _ (f.stalkMap_surjective x)

/-- If `Y âŸ¶ S` is surjective on stalks, then for every `X âŸ¶ S`, `X Ã—â‚› Y` is a subset of
`X Ã— Y` (cartesian product as topological spaces) with the induced topology. -/
lemma isEmbedding_pullback {X Y S : Scheme.{u}} (f : X âŸ¶ S) (g : Y âŸ¶ S) [SurjectiveOnStalks g] :
    IsEmbedding (fun x â†¦ ((pullback.fst f g).base x, (pullback.snd f g).base x)) := by
  let L := (fun x â†¦ ((pullback.fst f g).base x, (pullback.snd f g).base x))
  have H : âˆ€ R A B (f' : Spec A âŸ¶ Spec R) (g' : Spec B âŸ¶ Spec R) (iX : Spec A âŸ¶ X)
      (iY : Spec B âŸ¶ Y) (iS : Spec R âŸ¶ S) (eâ‚ eâ‚‚), IsOpenImmersion iX â†’ IsOpenImmersion iY â†’
      IsOpenImmersion iS â†’ IsEmbedding (L âˆ˜ (pullback.map f' g' f g iX iY iS eâ‚ eâ‚‚).base) := by
    intro R A B f' g' iX iY iS eâ‚ eâ‚‚ _ _ _
    have H : SurjectiveOnStalks g' :=
      have : SurjectiveOnStalks (g' â‰« iS) := eâ‚‚ â–¸ inferInstance
      .of_comp _ iS
    obtain âŸ¨Ï†, rflâŸ© : âˆƒ Ï†, Spec.map Ï† = f' := âŸ¨_, Spec.map_preimage _âŸ©
    obtain âŸ¨Ïˆ, rflâŸ© : âˆƒ Ïˆ, Spec.map Ïˆ = g' := âŸ¨_, Spec.map_preimage _âŸ©
    algebraize [Ï†.hom, Ïˆ.hom]
    rw [HasRingHomProperty.Spec_iff (P := @SurjectiveOnStalks)] at H
    convert ((iX.isOpenEmbedding.prodMap iY.isOpenEmbedding).isEmbedding.comp
      (PrimeSpectrum.isEmbedding_tensorProductTo_of_surjectiveOnStalks R A B H)).comp
      (Scheme.homeoOfIso (pullbackSpecIso R A B)).isEmbedding
    ext1 x
    obtain âŸ¨x, rflâŸ© := (Scheme.homeoOfIso (pullbackSpecIso R A B).symm).surjective x
    simp only [Scheme.homeoOfIso_apply, Function.comp_apply]
    ext
    Â· simp only [L, â† Scheme.comp_base_apply, pullback.lift_fst, Iso.symm_hom,
        Iso.inv_hom_id]
      erw [â† Scheme.comp_base_apply, pullbackSpecIso_inv_fst_assoc]
      rfl
    Â· simp only [L, â† Scheme.comp_base_apply, pullback.lift_snd, Iso.symm_hom,
        Iso.inv_hom_id]
      erw [â† Scheme.comp_base_apply, pullbackSpecIso_inv_snd_assoc]
      rfl
  let ğ’° := S.affineOpenCover.openCover
  let ğ’± (i) := ((ğ’°.pullbackCover f).obj i).affineOpenCover.openCover
  let ğ’² (i) := ((ğ’°.pullbackCover g).obj i).affineOpenCover.openCover
  let U (ijk : Î£ i, (ğ’± i).J Ã— (ğ’² i).J) : TopologicalSpace.Opens (X.carrier Ã— Y) :=
    âŸ¨{ P | P.1 âˆˆ ((ğ’± ijk.1).map ijk.2.1 â‰« (ğ’°.pullbackCover f).map ijk.1).opensRange âˆ§
          P.2 âˆˆ ((ğ’² ijk.1).map ijk.2.2 â‰« (ğ’°.pullbackCover g).map ijk.1).opensRange },
      (continuous_fst.1 _ ((ğ’± ijk.1).map ijk.2.1 â‰«
      (ğ’°.pullbackCover f).map ijk.1).opensRange.2).inter (continuous_snd.1 _
      ((ğ’² ijk.1).map ijk.2.2 â‰« (ğ’°.pullbackCover g).map ijk.1).opensRange.2)âŸ©
  have : Set.range L âŠ† (iSup U :) := by
    simp only [Scheme.Cover.pullbackCover_J, Scheme.Cover.pullbackCover_obj, Set.range_subset_iff]
    intro z
    simp only [SetLike.mem_coe, TopologicalSpace.Opens.mem_iSup, Sigma.exists, Prod.exists]
    obtain âŸ¨is, s, hsxâŸ© := ğ’°.exists_eq (f.base ((pullback.fst f g).base z))
    have hsy : (ğ’°.map is).base s = g.base ((pullback.snd f g).base z) := by
      rwa [â† Scheme.comp_base_apply, â† pullback.condition, Scheme.comp_base_apply]
    obtain âŸ¨x : (ğ’°.pullbackCover f).obj is, hxâŸ© :=
      Scheme.IsJointlySurjectivePreserving.exists_preimage_fst_triplet_of_prop
        (P := @IsOpenImmersion) inferInstance _ _ hsx.symm
    obtain âŸ¨y : (ğ’°.pullbackCover g).obj is, hyâŸ© :=
      Scheme.IsJointlySurjectivePreserving.exists_preimage_fst_triplet_of_prop
        (P := @IsOpenImmersion) inferInstance _ _ hsy.symm
    obtain âŸ¨ix, x, rflâŸ© := (ğ’± is).exists_eq x
    obtain âŸ¨iy, y, rflâŸ© := (ğ’² is).exists_eq y
    refine âŸ¨is, ix, iy, âŸ¨x, hxâŸ©, âŸ¨y, hyâŸ©âŸ©
  let ğ“¤ := (Scheme.Pullback.openCoverOfBase ğ’° f g).bind
    (fun i â†¦ Scheme.Pullback.openCoverOfLeftRight (ğ’± i) (ğ’² i) _ _)
  refine isEmbedding_of_iSup_eq_top_of_preimage_subset_range _ ?_ U this _ (fun i â†¦ (ğ“¤.map i).base)
    (fun i â†¦ (ğ“¤.map i).continuous) ?_ ?_
  Â· fun_prop
  Â· rintro i x âŸ¨âŸ¨xâ‚, hxâ‚âŸ©, âŸ¨xâ‚‚, hxâ‚‚âŸ©âŸ©
    obtain âŸ¨xâ‚', hxâ‚'âŸ© :=
      Scheme.IsJointlySurjectivePreserving.exists_preimage_fst_triplet_of_prop
        (P := @IsOpenImmersion) inferInstance _ _ hxâ‚.symm
    obtain âŸ¨xâ‚‚', hxâ‚‚'âŸ© :=
      Scheme.IsJointlySurjectivePreserving.exists_preimage_fst_triplet_of_prop
        (P := @IsOpenImmersion) inferInstance _ _ hxâ‚‚.symm
    obtain âŸ¨z, hzâŸ© :=
      Scheme.IsJointlySurjectivePreserving.exists_preimage_fst_triplet_of_prop
        (P := @IsOpenImmersion) inferInstance _ _ (hxâ‚'.trans hxâ‚‚'.symm)
    refine âŸ¨(pullbackFstFstIso _ _ _ _ _ _ (ğ’°.map i.1) ?_ ?_).hom.base z, ?_âŸ©
    Â· simp [pullback.condition]
    Â· simp [pullback.condition]
    Â· dsimp only
      rw [â† hxâ‚', â† hz, â† Scheme.comp_base_apply]
      erw [â† Scheme.comp_base_apply]
      congr 5
      apply pullback.hom_ext <;> simp [ğ“¤, â† pullback.condition, â† pullback.condition_assoc]
  Â· intro i
    have := H (S.affineOpenCover.obj i.1) (((ğ’°.pullbackCover f).obj i.1).affineOpenCover.obj i.2.1)
        (((ğ’°.pullbackCover g).obj i.1).affineOpenCover.obj i.2.2)
        ((ğ’± i.1).map i.2.1 â‰« ğ’°.pullbackHom f i.1)
        ((ğ’² i.1).map i.2.2 â‰« ğ’°.pullbackHom g i.1)
        ((ğ’± i.1).map i.2.1 â‰« (ğ’°.pullbackCover f).map i.1)
        ((ğ’² i.1).map i.2.2 â‰« (ğ’°.pullbackCover g).map i.1)
        (ğ’°.map i.1) (by simp [pullback.condition]) (by simp [pullback.condition])
        inferInstance inferInstance inferInstance
    convert this using 7
    apply pullback.hom_ext <;>
      simp [ğ“¤, Scheme.Cover.pullbackHom]

end SurjectiveOnStalks

end AlgebraicGeometry
