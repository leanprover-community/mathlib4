/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.AlgebraicGeometry.Morphisms.Affine
import Mathlib.AlgebraicGeometry.Morphisms.AffineAnd
import Mathlib.AlgebraicGeometry.Morphisms.LocalIso
import Mathlib.AlgebraicGeometry.Morphisms.RingHomProperties
import Mathlib.CategoryTheory.MorphismProperty.Descent

/-!
# Descent of morphism properties

Let `P` and `P'` be morphism properties. In this file we show some results to deduce
that `P` descends along `P'` from a codescent property of ring homomorphisms.

## Main results

- `HasRingHomProperty.descendsAlong`: if `P` is a local property induced by `Q`, `P'` implies
  `Q'` on global sections of affines and `Q` codescends along `Q'`, then `P` descends along `P'`.
- `HasAffineProperty.descendsAlong_of_affineAnd`: if `P` is given by `affineAnd Q`, `P'` implies
  `Q'` on global sections of affines and `Q` codescends along `Q'`, then `P` descends along `P'`
  (see TODOs).

## TODO

- Show that affine morphisms descend along faithfully-flat morphisms. This will make
  `HasAffineProperty.descendsAlong_of_affineAnd` useful.

-/

universe u v

open TensorProduct CategoryTheory Limits

namespace AlgebraicGeometry

variable (P P' : MorphismProperty Scheme.{u})

/--
If `P` is local at the source, every quasi-compact scheme is dominated by an
affine scheme via `p : Y ‚ü∂ X` such that `p` satisfies `P`.
-/
lemma Scheme.exists_hom_isAffine_of_isLocalAtSource (X : Scheme.{u}) [CompactSpace X]
    [IsLocalAtSource P] [P.ContainsIdentities] :
    ‚àÉ (Y : Scheme.{u}) (p : Y ‚ü∂ X), Surjective p ‚àß P p ‚àß IsAffine Y := by
  let ùí∞ := X.affineCover.finiteSubcover
  let p : ‚àê (fun i : ùí∞.I‚ÇÄ ‚Ü¶ ùí∞.X i) ‚ü∂ X := Sigma.desc (fun i ‚Ü¶ ùí∞.f i)
  refine ‚ü®_, p, ‚ü®fun x ‚Ü¶ ?_‚ü©, ?_, inferInstance‚ü©
  ¬∑ obtain ‚ü®i, x, rfl‚ü© := X.affineCover.finiteSubcover.exists_eq x
    use (Sigma.Œπ (fun i ‚Ü¶ X.affineCover.finiteSubcover.X i) i).base x
    rw [‚Üê Scheme.comp_base_apply, Sigma.Œπ_desc]
  ¬∑ rw [IsLocalAtSource.iff_of_openCover (P := P) (sigmaOpenCover _)]
    exact fun i ‚Ü¶ by simpa [p] using IsLocalAtSource.of_isOpenImmersion _

/-- If `P` is local at the target, to show `P` descends along `P'` we may assume
the base to be affine. -/
lemma IsLocalAtTarget.descendsAlong [IsLocalAtTarget P] [P'.IsStableUnderBaseChange]
    (H : ‚àÄ {R : CommRingCat.{u}} {X Y : Scheme.{u}} (f : X ‚ü∂ Spec R) (g : Y ‚ü∂ Spec R),
      P' f ‚Üí P (pullback.fst f g) ‚Üí P g) :
    P.DescendsAlong P' := by
  apply MorphismProperty.DescendsAlong.mk'
  introv h hf
  wlog hZ : ‚àÉ R, Z = Spec R generalizing X Y Z
  ¬∑ rw [IsLocalAtTarget.iff_of_openCover (P := P) Z.affineCover]
    intro i
    let Œπ := Z.affineCover.f i
    let e : pullback (pullback.snd f Œπ) (pullback.snd g Œπ) ‚âÖ
        pullback (pullback.fst f g) (pullback.fst f Œπ) :=
      pullbackLeftPullbackSndIso f Œπ (pullback.snd g Œπ) ‚â™‚â´
        pullback.congrHom rfl pullback.condition.symm ‚â™‚â´
        (pullbackAssoc f g g Œπ).symm ‚â™‚â´ pullback.congrHom pullback.condition.symm rfl ‚â™‚â´
        (pullbackRightPullbackFstIso f Œπ (pullback.fst f g)).symm
    have heq : e.hom ‚â´ pullback.snd (pullback.fst f g) (pullback.fst f Œπ) =
        pullback.fst (pullback.snd f Œπ) (pullback.snd g Œπ) := by
      apply pullback.hom_ext <;> simp [e, pullback.condition]
    refine this (f := pullback.snd f Œπ) ?_ ?_ ‚ü®_, rfl‚ü©
    ¬∑ exact P'.pullback_snd _ _ h
    ¬∑ change P (pullback.fst (pullback.snd f Œπ) (pullback.snd g Œπ))
      rw [‚Üê heq, P.cancel_left_of_respectsIso]
      exact AlgebraicGeometry.IsLocalAtTarget.of_isPullback (iY := pullback.fst f Œπ)
        (CategoryTheory.IsPullback.of_hasPullback _ _) hf
  obtain ‚ü®R, rfl‚ü© := hZ
  exact H f g h hf

variable (Q Q' : ‚àÄ {R S : Type u} [CommRing R] [CommRing S], (R ‚Üí+* S) ‚Üí Prop)

variable {Q Q'} in
lemma of_pullback_fst_Spec_of_codescendsAlong [P.RespectsIso]
    (hQQ' : RingHom.CodescendsAlong Q Q')
    (H‚ÇÅ : ‚àÄ {R S : CommRingCat.{u}} {f : R ‚ü∂ S}, P' (Spec.map f) ‚Üí Q' f.hom)
    (H‚ÇÇ : ‚àÄ {R S : CommRingCat.{u}} {f : R ‚ü∂ S}, P (Spec.map f) ‚Üî Q f.hom)
    {R S T : CommRingCat.{u}}
    {f : Spec T ‚ü∂ Spec R} {g : Spec S ‚ü∂ Spec R} (h : P' f) (hf : P (pullback.fst f g)) :
    P g := by
  obtain ‚ü®œÜ, rfl‚ü© := Spec.map_surjective g
  obtain ‚ü®œà, rfl‚ü© := Spec.map_surjective f
  algebraize [œÜ.hom, œà.hom]
  replace hf : P (pullback.fst (Spec.map <| CommRingCat.ofHom <| algebraMap R T)
    (Spec.map <| CommRingCat.ofHom <| algebraMap R S)) := hf
  rw [H‚ÇÇ]
  refine hQQ'.algebraMap_tensorProduct (R := R) (S := T) (T := S) _ (H‚ÇÅ h) ?_
  rwa [‚Üê pullbackSpecIso_hom_fst R T S, P.cancel_left_of_respectsIso, H‚ÇÇ] at hf

/-- If `X` admits a morphism `p : T ‚ü∂ X` from an affine scheme satisfying `P', to
show a property descends along a morphism `f : X ‚ü∂ Z` satisfying `P'`, `X` may assumed to
be affine. -/
lemma IsStableUnderBaseChange.of_pullback_fst_of_isAffine [P'.RespectsIso]
    [P'.IsStableUnderComposition] [P.IsStableUnderBaseChange]
    (H : ‚àÄ {R : CommRingCat.{u}} {S X : Scheme.{u}} (f : Spec R ‚ü∂ S) (g : X ‚ü∂ S),
      P' f ‚Üí P (pullback.fst f g) ‚Üí P g) {X Y Z T : Scheme.{u}} [IsAffine T] (p : T ‚ü∂ X)
    (hp : P' p) (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) (h : P' f) (hf : P (pullback.fst f g)) : P g := by
  apply H ((T.isoSpec.inv ‚â´ p) ‚â´ f)
  ¬∑ rw [Category.assoc, P'.cancel_left_of_respectsIso]
    exact P'.comp_mem _ _ hp h
  ¬∑ rw [‚Üê pullbackRightPullbackFstIso_inv_fst f g (T.isoSpec.inv ‚â´ p),
        P.cancel_left_of_respectsIso]
    exact P.pullback_fst _ _ hf

open Opposite

variable [P'.IsStableUnderBaseChange] [P'.IsStableUnderComposition] [P.IsStableUnderBaseChange]
variable
  (H‚ÇÅ : (@IsLocalIso ‚äì @Surjective : MorphismProperty Scheme) ‚â§ P')
  (H‚ÇÇ : ‚àÄ {R S : CommRingCat.{u}} {f : R ‚ü∂ S}, P' (Spec.map f) ‚Üí Q' f.hom)

include H‚ÇÅ in
lemma IsLocalAtTarget.descendsAlong_inf_quasiCompact [IsLocalAtTarget P]
    (H : ‚àÄ {R S : CommRingCat.{u}} {Y : Scheme.{u}} (œÜ : R ‚ü∂ S) (g : Y ‚ü∂ Spec R),
      P' (Spec.map œÜ) ‚Üí P (pullback.fst (Spec.map œÜ) g) ‚Üí P g) :
    P.DescendsAlong (P' ‚äì @QuasiCompact) := by
  apply IsLocalAtTarget.descendsAlong
  intro R X Y f g hf h
  wlog hX : ‚àÉ T, X = Spec T generalizing X
  ¬∑ have _ : CompactSpace X := by simpa [‚Üê quasiCompact_over_affine_iff f] using hf.2
    obtain ‚ü®Y, p, hsurj, hP', hY‚ü© := X.exists_hom_isAffine_of_isLocalAtSource @IsLocalIso
    refine this (f := (Y.isoSpec.inv ‚â´ p) ‚â´ f) ?_ ?_ ‚ü®_, rfl‚ü©
    ¬∑ rw [Category.assoc, (P' ‚äì @QuasiCompact).cancel_left_of_respectsIso]
      exact ‚ü®P'.comp_mem _ _ (H‚ÇÅ _ ‚ü®hP', hsurj‚ü©) hf.1, inferInstance‚ü©
    ¬∑ rw [‚Üê pullbackRightPullbackFstIso_inv_fst f g _, P.cancel_left_of_respectsIso]
      exact P.pullback_fst _ _ h
  obtain ‚ü®T, rfl‚ü© := hX
  obtain ‚ü®œÜ, rfl‚ü© := Spec.map_surjective f
  exact H œÜ g hf.1 h

include H‚ÇÅ H‚ÇÇ in
/--
Let `P` be the morphism property associated to the ring hom property `Q`. Suppose

- `P'` implies `Q'` on global sections for affine schemes,
- `P'` is satisfied for all surjective, local isomorphisms, and
- `Q` codescend along `Q'`.

Then `P` descends along quasi-compact morphisms satisfying `P'`.

Note: The second condition is in particular satisfied for faithfully flat morphisms.
-/
nonrec lemma HasRingHomProperty.descendsAlong [HasRingHomProperty P Q]
    (hQQ' : RingHom.CodescendsAlong Q Q') :
    P.DescendsAlong (P' ‚äì @QuasiCompact) := by
  apply IsLocalAtTarget.descendsAlong_inf_quasiCompact _ _ H‚ÇÅ
  introv h hf
  wlog hY : ‚àÉ S, Y = Spec S generalizing Y
  ¬∑ rw [IsLocalAtSource.iff_of_openCover (P := P) Y.affineCover]
    intro i
    have heq : pullback.fst (Spec.map œÜ) (Y.affineCover.f i ‚â´ g) =
        pullback.map _ _ _ _ (ùüô _) (Y.affineCover.f i) (ùüô _) (by simp) (by simp) ‚â´
          pullback.fst (Spec.map œÜ) g := (pullback.lift_fst _ _ _).symm
    exact this _ (heq ‚ñ∏ AlgebraicGeometry.IsLocalAtSource.comp hf _) ‚ü®_, rfl‚ü©
  obtain ‚ü®S, rfl‚ü© := hY
  apply of_pullback_fst_Spec_of_codescendsAlong _ _ hQQ' H‚ÇÇ _ h hf
  simp [HasRingHomProperty.Spec_iff (P := P)]

include H‚ÇÅ H‚ÇÇ in
/--
Let `P` be a morphism property associated with `affineAnd Q`. Suppose

- `P'` implies `Q'` on global sections on affine schemes,
- `P'` is satisfied for surjective, local isomorphisms,
- affine morphisms descend along `P''`, and
- `Q` codescends along `Q'`,

Then `P` descends along quasi-compact morphisms satisfying `P'`.

Note: The second condition is in particular satisfied for faithfully flat morphisms.
-/
nonrec lemma HasAffineProperty.descendsAlong_of_affineAnd
    (hP : HasAffineProperty P (affineAnd Q)) [MorphismProperty.DescendsAlong @IsAffineHom P']
    (hQ : RingHom.RespectsIso Q) (hQQ' : RingHom.CodescendsAlong Q Q') :
    P.DescendsAlong (P' ‚äì @QuasiCompact) := by
  apply IsLocalAtTarget.descendsAlong_inf_quasiCompact _ _ H‚ÇÅ
  introv h hf
  have : IsAffine Y := by
    convert isAffine_of_isAffineHom g
    exact MorphismProperty.of_pullback_fst_of_descendsAlong h <|
      AlgebraicGeometry.HasAffineProperty.affineAnd_le_isAffineHom P inferInstance _ hf
  wlog hY : ‚àÉ S, Y = Spec S generalizing Y
  ¬∑ rw [‚Üê P.cancel_left_of_respectsIso Y.isoSpec.inv]
    have heq : pullback.fst (Spec.map œÜ) (Y.isoSpec.inv ‚â´ g) =
        pullback.map _ _ _ _ (ùüô _) (Y.isoSpec.inv) (ùüô _) (by simp) (by simp) ‚â´
          pullback.fst (Spec.map œÜ) g := (pullback.lift_fst _ _ _).symm
    refine this _ ?_ inferInstance ‚ü®_, rfl‚ü©
    rwa [heq, P.cancel_left_of_respectsIso]
  obtain ‚ü®Y, rfl‚ü© := hY
  apply of_pullback_fst_Spec_of_codescendsAlong _ _ hQQ' H‚ÇÇ _ h hf
  simp [SpecMap_iff_of_affineAnd _ hQ]

end AlgebraicGeometry
