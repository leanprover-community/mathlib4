/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.Basic
import Mathlib.RingTheory.LocalProperties

/-!

# Properties of morphisms from properties of ring homs.

We provide the basic framework for talking about properties of morphisms that come from properties
of ring homs. For `P` a property of ring homs, we have two ways of defining a property of scheme
morphisms:

Let `f : X ‚ü∂ Y`,
- `targetAffineLocally (affine_and P)`: the preimage of an affine open `U = Spec A` is affine
  (`= Spec B`) and `A ‚ü∂ B` satisfies `P`. (TODO)
- `affineLocally P`: For each pair of affine open `U = Spec A ‚äÜ X` and `V = Spec B ‚äÜ f ‚Åª¬π' U`,
  the ring hom `A ‚ü∂ B` satisfies `P`.

For these notions to be well defined, we require `P` be a sufficient local property. For the former,
`P` should be local on the source (`RingHom.RespectsIso P`, `RingHom.LocalizationPreserves P`,
`RingHom.OfLocalizationSpan`), and `targetAffineLocally (affine_and P)` will be local on
the target. (TODO)

For the latter `P` should be local on the target (`RingHom.PropertyIsLocal P`), and
`affineLocally P` will be local on both the source and the target.

Further more, these properties are stable under compositions (resp. base change) if `P` is. (TODO)

-/

-- Explicit universe annotations were used in this file to improve perfomance #12737

universe u

open CategoryTheory Opposite TopologicalSpace CategoryTheory.Limits AlgebraicGeometry

variable (P : ‚àÄ {R S : Type u} [CommRing R] [CommRing S], (R ‚Üí+* S) ‚Üí Prop)

namespace RingHom

variable {P}
theorem RespectsIso.basicOpen_iff (hP : RespectsIso @P) {X Y : Scheme.{u}} [IsAffine X] [IsAffine Y]
    (f : X ‚ü∂ Y) (r : Y.presheaf.obj (Opposite.op ‚ä§)) :
    P (Scheme.Œì.map (f ‚à£_ Y.basicOpen r).op) ‚Üî
    P (@IsLocalization.Away.map (Y.presheaf.obj (Opposite.op ‚ä§)) _
      (Y.presheaf.obj (Opposite.op <| Y.basicOpen r)) _ _ (X.presheaf.obj (Opposite.op ‚ä§)) _
      (X.presheaf.obj (Opposite.op <| X.basicOpen (Scheme.Œì.map f.op r))) _ _
      (Scheme.Œì.map f.op) r _ <| @isLocalization_away_of_isAffine X _ (Scheme.Œì.map f.op r)) := by
  rw [Œì_map_morphismRestrict, hP.cancel_left_isIso, hP.cancel_right_isIso,
    ‚Üê hP.cancel_right_isIso (f.val.c.app (Opposite.op (Y.basicOpen r)))
      (X.presheaf.map (eqToHom (Scheme.preimage_basicOpen f r).symm).op), ‚Üê eq_iff_iff]
  congr
  delta IsLocalization.Away.map
  refine IsLocalization.ringHom_ext (Submonoid.powers r) ?_
  generalize_proofs
  haveI i1 := @isLocalization_away_of_isAffine X _ (Scheme.Œì.map f.op r)
  -- Porting note: needs to be very explicit here
  convert
    (@IsLocalization.map_comp (hy := ‚Äπ_ ‚â§ _‚Ä∫) (Y.presheaf.obj <| Opposite.op (Scheme.basicOpen Y r))
    _ _ (isLocalization_away_of_isAffine _) _ _ _ i1).symm using 1
  change Y.presheaf.map _ ‚â´ _ = _ ‚â´ X.presheaf.map _
  rw [f.val.c.naturality_assoc]
  dsimp
  simp only [‚Üê X.presheaf.map_comp]
  congr 1

theorem RespectsIso.basicOpen_iff_localization (hP : RespectsIso @P) {X Y : Scheme.{u}} [IsAffine X]
    [IsAffine Y] (f : X ‚ü∂ Y) (r : Y.presheaf.obj (Opposite.op ‚ä§)) :
    P (Scheme.Œì.map (f ‚à£_ Y.basicOpen r).op) ‚Üî P (Localization.awayMap (Scheme.Œì.map f.op) r) := by
  refine (hP.basicOpen_iff _ _).trans ?_
  -- Porting note: was a one line term mode proof, but this `dsimp` is vital so the term mode
  -- one liner is not possible
  dsimp
  rw [‚Üê hP.is_localization_away_iff]

@[deprecated (since := "2024-03-02")] alias
RespectsIso.ofRestrict_morphismRestrict_iff_of_isAffine := RespectsIso.basicOpen_iff_localization

theorem RespectsIso.ofRestrict_morphismRestrict_iff (hP : RingHom.RespectsIso @P) {X Y : Scheme.{u}}
    [IsAffine Y] (f : X ‚ü∂ Y) (r : Y.presheaf.obj (Opposite.op ‚ä§)) (U : X.Opens)
    (hU : IsAffineOpen U) {V : Scheme.Opens _}
    (e : V = (Scheme.Opens.Œπ <| f ‚Åª¬π·µÅ Y.basicOpen r) ‚Åª¬π·µÅ U) :
    P (Scheme.Œì.map (V.Œπ ‚â´ f ‚à£_ Y.basicOpen r).op) ‚Üî
    P (Localization.awayMap (Scheme.Œì.map (U.Œπ ‚â´ f).op) r) := by
  subst e
  refine (hP.cancel_right_isIso _
    (Scheme.Œì.mapIso (Scheme.restrictRestrictComm _ _ _).op).inv).symm.trans ?_
  haveI : IsAffine _ := hU
  rw [‚Üê hP.basicOpen_iff_localization, iff_iff_eq]
  congr 1
  simp only [Functor.mapIso_inv, Iso.op_inv, ‚Üê Functor.map_comp, ‚Üê op_comp, morphismRestrict_comp]
  rw [‚Üê Category.assoc]
  congr 3
  rw [‚Üê cancel_mono (Scheme.Opens.Œπ _), Category.assoc, Scheme.restrictRestrictComm,
    IsOpenImmersion.isoOfRangeEq_inv_fac, morphismRestrict_Œπ]

theorem StableUnderBaseChange.Œì_pullback_fst (hP : StableUnderBaseChange @P) (hP' : RespectsIso @P)
    {X Y S : Scheme} [IsAffine X] [IsAffine Y] [IsAffine S] (f : X ‚ü∂ S) (g : Y ‚ü∂ S)
    (H : P (Scheme.Œì.map g.op)) : P (Scheme.Œì.map (pullback.fst f g).op) := by
  -- Porting note (#11224): change `rw` to `erw`
  erw [‚Üê PreservesPullback.iso_inv_fst AffineScheme.forgetToScheme (AffineScheme.ofHom f)
      (AffineScheme.ofHom g)]
  rw [op_comp, Functor.map_comp, hP'.cancel_right_isIso, AffineScheme.forgetToScheme_map]
  have :=
    _root_.congr_arg Quiver.Hom.unop
      (PreservesPullback.iso_hom_fst AffineScheme.Œì.rightOp (AffineScheme.ofHom f)
        (AffineScheme.ofHom g))
  simp only [Quiver.Hom.unop_op, Functor.rightOp_map, unop_comp] at this
  delta AffineScheme.Œì at this
  simp only [Quiver.Hom.unop_op, Functor.comp_map, AffineScheme.forgetToScheme_map,
    Functor.op_map] at this
  rw [‚Üê this, hP'.cancel_right_isIso, ‚Üê pushoutIsoUnopPullback_inl_hom, hP'.cancel_right_isIso]
  exact hP.pushout_inl _ hP' _ _ H

end RingHom

namespace AlgebraicGeometry

/-- For `P` a property of ring homomorphisms, `sourceAffineLocally P` holds for `f : X ‚ü∂ Y`
whenever `P` holds for the restriction of `f` on every affine open subset of `X`. -/
def sourceAffineLocally : AffineTargetMorphismProperty := fun X _ f _ =>
  ‚àÄ U : X.affineOpens, P (Scheme.Œì.map (U.1.Œπ ‚â´ f).op)

/-- For `P` a property of ring homomorphisms, `affineLocally P` holds for `f : X ‚ü∂ Y` if for each
affine open `U = Spec A ‚äÜ Y` and `V = Spec B ‚äÜ f ‚Åª¬π' U`, the ring hom `A ‚ü∂ B` satisfies `P`.
Also see `affineLocally_iff_affineOpens_le`. -/
abbrev affineLocally : MorphismProperty Scheme.{u} :=
  targetAffineLocally (sourceAffineLocally @P)

variable {P}

theorem sourceAffineLocally_respectsIso (h‚ÇÅ : RingHom.RespectsIso @P) :
    (sourceAffineLocally @P).toProperty.RespectsIso := by
  apply AffineTargetMorphismProperty.respectsIso_mk
  ¬∑ introv H U
    rw [‚Üê h‚ÇÅ.cancel_right_isIso _ (Scheme.Œì.map (Scheme.restrictMapIso e.inv U.1).hom.op), ‚Üê
      Functor.map_comp, ‚Üê op_comp]
    convert H ‚ü®_, U.prop.preimage_of_isIso e.inv‚ü© using 3
    rw [IsOpenImmersion.isoOfRangeEq_hom_fac_assoc, Category.assoc,
      e.inv_hom_id_assoc]
  ¬∑ introv H U
    rw [‚Üê Category.assoc, op_comp, Functor.map_comp, h‚ÇÅ.cancel_left_isIso]
    exact H U

theorem affineLocally_respectsIso (h : RingHom.RespectsIso @P) : (affineLocally @P).RespectsIso := by
  have := sourceAffineLocally_respectsIso h
  delta affineLocally
  infer_instance

theorem affineLocally_iff_affineOpens_le
    (hP : RingHom.RespectsIso @P) {X Y : Scheme.{u}} (f : X ‚ü∂ Y) :
    affineLocally.{u} (@P) f ‚Üî
    ‚àÄ (U : Y.affineOpens) (V : X.affineOpens) (e : V.1 ‚â§ (Opens.map f.1.base).obj U.1),
      P (f.appLE _ _ e) := by
  apply forall_congr'
  intro U
  delta sourceAffineLocally
  simp_rw [op_comp, Scheme.Œì.map_comp, Œì_map_morphismRestrict, Category.assoc, Scheme.Œì_map_op,
    hP.cancel_left_isIso (Y.presheaf.map (eqToHom _).op)]
  constructor
  ¬∑ intro H V e
    let U' := f ‚Åª¬π·µÅ U.1
    have e'' : U'.Œπ ''·µÅ U'.Œπ ‚Åª¬π·µÅ V = V := by
      ext1; refine Set.image_preimage_eq_inter_range.trans (Set.inter_eq_left.mpr ?_)
      erw [Subtype.range_val]
      exact e
    have h : U'.Œπ ‚Åª¬π·µÅ ‚ÜëV ‚àà Scheme.affineOpens _ := by
      apply U'.Œπ.isAffineOpen_iff_of_isOpenImmersion.mp
      -- Porting note: was convert V.2
      rw [e'']
      convert V.2
    have := H ‚ü®U'.Œπ ‚Åª¬π·µÅ V.1, h‚ü©
    simp only [Scheme.Œì_obj, Opens.coe_inclusion, eqToHom_op, Scheme.Opens.Œπ_app, Opens.map_top,
      Functor.op_obj, Opens.carrier_eq_coe, Opens.coe_top, Set.preimage_univ,
      Scheme.Opens.toScheme_presheaf_map, ‚Üê Functor.map_comp] at this
    rw [‚Üê hP.cancel_right_isIso _ (X.presheaf.map (eqToHom _)), Scheme.Hom.appLE,
      Category.assoc, ‚Üê X.presheaf.map_comp]
    ¬∑ convert this using 1
    ¬∑ dsimp only [Functor.op, unop_op]
      rw [Opens.openEmbedding_obj_top]
      congr 1
      exact e''.symm
  ¬∑ intro H V
    specialize H ‚ü®_, V.2.image_of_isOpenImmersion (Scheme.Opens.Œπ _)‚ü© (Subtype.coe_image_subset _ _)
    simp only [Scheme.Œì_obj, Opens.coe_inclusion, eqToHom_op, Scheme.Opens.Œπ_app, Opens.map_top,
      Functor.op_obj, Opens.carrier_eq_coe, Opens.coe_top, Set.preimage_univ,
      Scheme.Opens.toScheme_presheaf_map, Scheme.Hom.appLE] at H ‚ä¢
    rw [‚Üê hP.cancel_right_isIso _ (X.presheaf.map (eqToHom _)), Category.assoc]
    ¬∑ convert H
      simp only [Scheme.ofRestrict_val_c_app, Scheme.restrict_presheaf_map, ‚Üê X.presheaf.map_comp]
      congr 1
    ¬∑ dsimp only [Functor.op, unop_op]; rw [Opens.openEmbedding_obj_top]

-- The `IsLocalization` is not necessary, but Lean errors if it is omitted.
@[nolint unusedHavesSuffices]
theorem scheme_restrict_basicOpen_of_localizationPreserves (h‚ÇÅ : RingHom.RespectsIso @P)
    (h‚ÇÇ : RingHom.LocalizationPreserves @P) {X Y : Scheme.{u}} [IsAffine Y] (f : X ‚ü∂ Y)
    (r : Y.presheaf.obj (op ‚ä§)) (H : sourceAffineLocally (@P) f)
    (U : (X.restrict ((Opens.map f.1.base).obj <| Y.basicOpen r).openEmbedding).affineOpens) :
    P (Scheme.Œì.map ((X.restrict ((Opens.map f.1.base).obj <|
      Y.basicOpen r).openEmbedding).ofRestrict U.1.openEmbedding ‚â´ f ‚à£_ Y.basicOpen r).op) := by
  specialize H ‚ü®_, U.2.image_of_isOpenImmersion (X.ofRestrict _)‚ü©
  letI i1 : Algebra (Y.presheaf.obj <| Opposite.op ‚ä§) (Localization.Away r) :=
    OreLocalization.instAlgebra
  haveI : IsLocalization.Away r (Localization.Away r) := inferInstance
  exact (h‚ÇÅ.ofRestrict_morphismRestrict_iff f r
    ((Scheme.Hom.opensFunctor
      (X.ofRestrict ((Opens.map f.1.base).obj <| Y.basicOpen r).openEmbedding)).obj U.1)
    (IsAffineOpen.image_of_isOpenImmersion U.2
      (X.ofRestrict ((Opens.map f.1.base).obj <| Y.basicOpen r).openEmbedding))
    (Opens.ext (Set.preimage_image_eq _ Subtype.coe_injective).symm)).mpr (h‚ÇÇ.away r H)

theorem sourceAffineLocally_isLocal (h‚ÇÅ : RingHom.RespectsIso @P)
    (h‚ÇÇ : RingHom.LocalizationPreserves @P) (h‚ÇÉ : RingHom.OfLocalizationSpan @P) :
    (sourceAffineLocally @P).IsLocal := by
  constructor
  ¬∑ exact sourceAffineLocally_respectsIso h‚ÇÅ
  ¬∑ introv H U
    apply scheme_restrict_basicOpen_of_localizationPreserves h‚ÇÅ h‚ÇÇ; assumption
  ¬∑ introv hs hs' U
    apply h‚ÇÉ _ _ hs
    intro r
    have := hs' r ‚ü®(Opens.map (X.ofRestrict _).1.base).obj U.1, ?_‚ü©
    ¬∑ rwa [h‚ÇÅ.ofRestrict_morphismRestrict_iff] at this
      ¬∑ exact U.2
      ¬∑ rfl
    ¬∑ suffices ‚àÄ (V) (_ : V = (Opens.map f.val.base).obj (Y.basicOpen r.val)),
          IsAffineOpen ((Opens.map (X.ofRestrict V.openEmbedding).1.base).obj U.1) by
        exact this _ rfl
      intro V hV
      rw [Scheme.preimage_basicOpen] at hV
      subst hV
      exact U.2.Œπ_basicOpen_preimage (Scheme.Œì.map f.op r.1)

variable (hP : RingHom.PropertyIsLocal @P)

theorem sourceAffineLocally_of_source_open_cover_aux (h‚ÇÅ : RingHom.RespectsIso @P)
    (h‚ÇÉ : RingHom.OfLocalizationSpanTarget @P) {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : X.affineOpens)
    (s : Set (X.presheaf.obj (op U.1))) (hs : Ideal.span s = ‚ä§)
    (hs' : ‚àÄ r : s, P (Scheme.Œì.map ((X.basicOpen r.1).Œπ ‚â´ f).op)) :
    P (Scheme.Œì.map (U.1.Œπ ‚â´ f).op) := by
  apply_fun Ideal.map (X.presheaf.map (eqToHom U.1.openEmbedding_obj_top).op) at hs
  rw [Ideal.map_span, Ideal.map_top] at hs
  apply h‚ÇÉ.ofIsLocalization h‚ÇÅ _ _ hs
  rintro ‚ü®s, r, hr, hs‚ü©
  refine ‚ü®_, _, _, @AlgebraicGeometry.Œì_restrict_isLocalization U.1 U.2 s, ?_‚ü©
  rw [RingHom.algebraMap_toAlgebra, ‚Üê CommRingCat.comp_eq_ring_hom_comp, ‚Üê Functor.map_comp,
    ‚Üê op_comp, ‚Üê h‚ÇÅ.cancel_right_isIso _ (Scheme.Œì.mapIso (Scheme.restrictRestrict _ _ _).op).inv]
  subst hs
  rw [‚Üê h‚ÇÅ.cancel_right_isIso _
    (Scheme.Œì.mapIso (Scheme.restrictIsoOfEq _ (Scheme.map_basicOpen_map _ _)).op).inv]
  simp only [Functor.mapIso_inv, Iso.op_inv, ‚Üê Functor.map_comp, ‚Üê op_comp,
    Scheme.restrictRestrict_inv_restrict_restrict_assoc, Scheme.restrictIsoOfEq]
  erw [IsOpenImmersion.isoOfRangeEq_inv_fac_assoc]
  exact hs' ‚ü®r, hr‚ü©

theorem isOpenImmersionCat_comp_of_sourceAffineLocally (h‚ÇÅ : RingHom.RespectsIso @P)
    {X Y Z : Scheme.{u}} [IsAffine X] [IsAffine Z] (f : X ‚ü∂ Y) [IsOpenImmersion f] (g : Y ‚ü∂ Z)
    (h‚ÇÇ : sourceAffineLocally (@P) g) : P (Scheme.Œì.map (f ‚â´ g).op) := by
  rw [‚Üê h‚ÇÅ.cancel_right_isIso _
    (Scheme.Œì.map (IsOpenImmersion.isoOfRangeEq f.opensRange.Œπ f _).hom.op),
    ‚Üê Functor.map_comp, ‚Üê op_comp]
  ¬∑ convert h‚ÇÇ ‚ü®_, isAffineOpen_opensRange f‚ü© using 3
    ¬∑ rw [IsOpenImmersion.isoOfRangeEq_hom_fac_assoc]
      exact Subtype.range_coe

end AlgebraicGeometry

open AlgebraicGeometry

namespace RingHom.PropertyIsLocal

variable {P} (hP : RingHom.PropertyIsLocal @P)

theorem sourceAffineLocally_of_source_openCover {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [IsAffine Y]
    (ùí∞ : X.OpenCover) [‚àÄ i, IsAffine (ùí∞.obj i)] (H : ‚àÄ i, P (Scheme.Œì.map (ùí∞.map i ‚â´ f).op)) :
    sourceAffineLocally (@P) f := by
  let S i := (‚ü®‚ü®Set.range (ùí∞.map i).1.base, (ùí∞.IsOpen i).base_open.isOpen_range‚ü©,
    isAffineOpen_opensRange (ùí∞.map i)‚ü© : X.affineOpens)
  intro U
  -- Porting note: here is what we are eliminating into Lean
  apply of_affine_open_cover
    (P := fun V => P (Scheme.Œì.map (X.ofRestrict (Opens.openEmbedding V.val) ‚â´ f).op)) S
    ùí∞.iSup_opensRange
  ¬∑ intro U r H
    -- Porting note: failing on instance synthesis for an (unspecified) meta variable
    -- made œÜ explicit and forced to use dsimp in the proof
    convert hP.StableUnderComposition
      (S := Scheme.Œì.obj (Opposite.op (X.restrict <| Opens.openEmbedding U.val)))
      (T := Scheme.Œì.obj (Opposite.op (X.restrict <| Opens.openEmbedding (X.basicOpen r))))
      ?_ ?_ H ?_ using 1
    swap
    ¬∑ refine X.presheaf.map
          (@homOfLE _ _ ((IsOpenMap.functor _).obj _) ((IsOpenMap.functor _).obj _) ?_).op
      dsimp
      rw [Opens.openEmbedding_obj_top, Opens.openEmbedding_obj_top]
      exact X.basicOpen_le _
    ¬∑ rw [op_comp, op_comp, Functor.map_comp, Functor.map_comp]
      refine (Eq.trans ?_ (Category.assoc (obj := CommRingCat) _ _ _).symm : _)
      congr 1
      dsimp
      refine Eq.trans ?_ (X.presheaf.map_comp _ _)
      change X.presheaf.map _ = _
      congr!
    -- Porting note: need to pass Algebra through explicitly
    convert @HoldsForLocalizationAway _ hP _
      (Scheme.Œì.obj (Opposite.op (X.restrict (X.basicOpen r).openEmbedding))) _ _ ?_
      (X.presheaf.map (eqToHom U.1.openEmbedding_obj_top).op r) ?_
    ¬∑ exact RingHom.algebraMap_toAlgebra
        (R := Scheme.Œì.obj <| Opposite.op <| X.restrict (U.1.openEmbedding))
        (S :=
          Scheme.Œì.obj (Opposite.op <| X.restrict (X.affineBasicOpen r).1.openEmbedding)) _|>.symm
    ¬∑ dsimp [Scheme.Œì]
      have := U.2
      rw [‚Üê U.1.openEmbedding_obj_top] at this
      -- Porting note: the second argument of `IsLocalization.Away` is a type, and we want
      -- to generate an equality, so using `typeEqs := true` to force allowing type equalities.
      convert (config := {typeEqs := true, transparency := .default})
          this.isLocalization_basicOpen _ using 5
      all_goals rw [Opens.openEmbedding_obj_top]; exact (Scheme.basicOpen_res_eq _ _ _).symm
  ¬∑ introv hs hs'
    exact sourceAffineLocally_of_source_open_cover_aux hP.respectsIso hP.2 _ _ _ hs hs'
  ¬∑ rintro i
    specialize H i
    rw [‚Üê hP.respectsIso.cancel_right_isIso _
        (Scheme.Œì.map
          (IsOpenImmersion.isoOfRangeEq (ùí∞.map i) (X.ofRestrict (S i).1.openEmbedding)
                Subtype.range_coe.symm).inv.op)] at H
    rwa [‚Üê Scheme.Œì.map_comp, ‚Üê op_comp, IsOpenImmersion.isoOfRangeEq_inv_fac_assoc] at H

theorem affine_openCover_TFAE {X Y : Scheme.{u}} [IsAffine Y] (f : X ‚ü∂ Y) :
    List.TFAE
      [sourceAffineLocally (@P) f,
        ‚àÉ (ùí∞ : Scheme.OpenCover.{u} X) (_ : ‚àÄ i, IsAffine (ùí∞.obj i)),
          ‚àÄ i : ùí∞.J, P (Scheme.Œì.map (ùí∞.map i ‚â´ f).op),
        ‚àÄ (ùí∞ : Scheme.OpenCover.{u} X) [‚àÄ i, IsAffine (ùí∞.obj i)] (i : ùí∞.J),
          P (Scheme.Œì.map (ùí∞.map i ‚â´ f).op),
        ‚àÄ {U : Scheme} (g : U ‚ü∂ X) [IsAffine U] [IsOpenImmersion g],
          P (Scheme.Œì.map (g ‚â´ f).op)] := by
  tfae_have 1 ‚Üí 4
  ¬∑ intro H U g _ hg
    specialize H ‚ü®g.opensRange, isAffineOpen_opensRange g‚ü©
    rw [‚Üê hP.respectsIso.cancel_right_isIso _ (Scheme.Œì.map (IsOpenImmersion.isoOfRangeEq g
      g.opensRange.Œπ Subtype.range_coe.symm).hom.op),
      ‚Üê Scheme.Œì.map_comp, ‚Üê op_comp, IsOpenImmersion.isoOfRangeEq_hom_fac_assoc] at H
    exact H
  tfae_have 4 ‚Üí 3
  ¬∑ intro H ùí∞ _ i; apply H
  tfae_have 3 ‚Üí 2
  ¬∑ intro H; exact ‚ü®X.affineCover, inferInstance, H _‚ü©
  tfae_have 2 ‚Üí 1
  ¬∑ rintro ‚ü®ùí∞, _, hùí∞‚ü©
    exact sourceAffineLocally_of_source_openCover hP f ùí∞ hùí∞
  tfae_finish

theorem openCover_TFAE {X Y : Scheme.{u}} [IsAffine Y] (f : X ‚ü∂ Y) :
    List.TFAE
      [sourceAffineLocally (@P) f,
        ‚àÉ ùí∞ : Scheme.OpenCover.{u} X, ‚àÄ i : ùí∞.J, sourceAffineLocally (@P) (ùí∞.map i ‚â´ f),
        ‚àÄ (ùí∞ : Scheme.OpenCover.{u} X) (i : ùí∞.J), sourceAffineLocally (@P) (ùí∞.map i ‚â´ f),
        ‚àÄ {U : Scheme} (g : U ‚ü∂ X) [IsOpenImmersion g], sourceAffineLocally (@P) (g ‚â´ f)] := by
  tfae_have 1 ‚Üí 4
  ¬∑ intro H U g hg V
    -- Porting note: this has metavariable if I put it directly into rw
    have := (hP.affine_openCover_TFAE f).out 0 3
    rw [this] at H
    haveI : IsAffine _ := V.2
    rw [‚Üê Category.assoc]
    apply H
  tfae_have 4 ‚Üí 3
  ¬∑ intro H ùí∞ _ i; apply H
  tfae_have 3 ‚Üí 2
  ¬∑ intro H; exact ‚ü®X.affineCover, H _‚ü©
  tfae_have 2 ‚Üí 1
  ¬∑ rintro ‚ü®ùí∞, hùí∞‚ü©
    -- Porting note: this has metavariable if I put it directly into rw
    have := (hP.affine_openCover_TFAE f).out 0 1
    rw [this]
    refine ‚ü®ùí∞.bind fun _ => Scheme.affineCover _, ?_, ?_‚ü©
    ¬∑ intro i; dsimp; infer_instance
    ¬∑ intro i
      specialize hùí∞ i.1
      -- Porting note: this has metavariable if I put it directly into rw
      have := (hP.affine_openCover_TFAE (ùí∞.map i.fst ‚â´ f)).out 0 3
      rw [this] at hùí∞
      -- Porting note: this was discharged after the apply previously
      have : IsAffine (Scheme.OpenCover.obj
        (Scheme.OpenCover.bind ùí∞ fun x ‚Ü¶ Scheme.affineCover (Scheme.OpenCover.obj ùí∞ x)) i) := by
          dsimp; infer_instance
      apply @hùí∞ _ (show _ from _)
  tfae_finish

theorem sourceAffineLocally_comp_of_isOpenImmersion {X Y Z : Scheme.{u}} [IsAffine Z] (f : X ‚ü∂ Y)
    (g : Y ‚ü∂ Z) [IsOpenImmersion f] (H : sourceAffineLocally (@P) g) :
    sourceAffineLocally (@P) (f ‚â´ g) := by
      -- Porting note: more tfae mis-behavior
      have := (hP.openCover_TFAE g).out 0 3
      apply this.mp H

theorem source_affine_openCover_iff {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [IsAffine Y]
    (ùí∞ : Scheme.OpenCover.{u} X) [‚àÄ i, IsAffine (ùí∞.obj i)] :
    sourceAffineLocally (@P) f ‚Üî ‚àÄ i, P (Scheme.Œì.map (ùí∞.map i ‚â´ f).op) := by
  -- Porting note: seems like TFAE is misbehaving; this used to be pure term proof but
  -- had strange failures where the output of TFAE turned into a metavariable when used despite
  -- being correctly displayed in the infoview
  refine ‚ü®fun H => ?_, fun H => ?_‚ü©
  ¬∑ have h := (hP.affine_openCover_TFAE f).out 0 2
    apply h.mp
    exact H
  ¬∑ have h := (hP.affine_openCover_TFAE f).out 1 0
    apply h.mp
    use ùí∞

theorem isLocal_sourceAffineLocally : (sourceAffineLocally @P).IsLocal :=
  sourceAffineLocally_isLocal hP.respectsIso hP.LocalizationPreserves
    (@RingHom.PropertyIsLocal.ofLocalizationSpan _ hP)

theorem hasAffinePropertyAffineLocally :
    HasAffineProperty (affineLocally @P) (sourceAffineLocally @P) where
  isLocal_affineProperty := hP.isLocal_sourceAffineLocally
  eq_targetAffineLocally' := rfl

theorem affine_openCover_iff {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (ùí∞ : Scheme.OpenCover.{u} Y)
    [‚àÄ i, IsAffine (ùí∞.obj i)] (ùí∞' : ‚àÄ i, Scheme.OpenCover.{u} ((ùí∞.pullbackCover f).obj i))
    [‚àÄ i j, IsAffine ((ùí∞' i).obj j)] :
    affineLocally (@P) f ‚Üî ‚àÄ i j, P (Scheme.Œì.map ((ùí∞' i).map j ‚â´ pullback.snd _ _).op) :=
  letI := hP.hasAffinePropertyAffineLocally
  (HasAffineProperty.iff_of_openCover ùí∞).trans
    (forall_congr' fun i => hP.source_affine_openCover_iff _ (ùí∞' i))

theorem source_openCover_iff {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (ùí∞ : Scheme.OpenCover.{u} X) :
    affineLocally (@P) f ‚Üî ‚àÄ i, affineLocally (@P) (ùí∞.map i ‚â´ f) := by
  constructor
  ¬∑ intro H i U
    rw [morphismRestrict_comp]
    apply hP.sourceAffineLocally_comp_of_isOpenImmersion
    apply H
  ¬∑ intro H U
    haveI : IsAffine _ := U.2
    apply ((hP.openCover_TFAE (f ‚à£_ U.1)).out 1 0).mp
    use ùí∞.pullbackCover (X.ofRestrict _)
    intro i
    specialize H i U
    rw [morphismRestrict_comp] at H
    delta morphismRestrict at H
    have := sourceAffineLocally_respectsIso hP.respectsIso
    rw [Category.assoc, (sourceAffineLocally P).cancel_left_of_respectsIso,
      ‚Üê (sourceAffineLocally P).cancel_left_of_respectsIso (pullbackSymmetry _ _).hom,
      pullbackSymmetry_hom_comp_snd_assoc] at H
    exact H

theorem affineLocally_of_isOpenImmersion (hP : RingHom.PropertyIsLocal @P) {X Y : Scheme.{u}}
    (f : X ‚ü∂ Y) [hf : IsOpenImmersion f] : affineLocally (@P) f := by
  intro U
  haveI H : IsAffine _ := U.2
  rw [‚Üê Category.comp_id (f ‚à£_ U)]
  apply hP.sourceAffineLocally_comp_of_isOpenImmersion
  -- Porting note: need to excuse Lean from synthesizing an instance
  rw [@source_affine_openCover_iff _ hP _ _ _ _ (Scheme.openCoverOfIsIso (ùüô _)) (_)]
  ¬∑ intro i
    let esto := Scheme.Œì.obj (Opposite.op (Y.restrict <| Opens.openEmbedding U.val))
    let eso := Scheme.Œì.obj (Opposite.op ((Scheme.openCoverOfIsIso
      (ùüô (Y.restrict <| Opens.openEmbedding U.val))).obj i))
    have := hP.HoldsForLocalizationAway
    convert @this esto eso _ _ ?_ 1 ?_
    ¬∑ exact (RingHom.algebraMap_toAlgebra (Scheme.Œì.map _)).symm
    ¬∑ exact
        @IsLocalization.away_of_isUnit_of_bijective _ _ _ _ (_) _ isUnit_one Function.bijective_id
  ¬∑ intro; exact H

theorem affineLocally_of_comp
    (H : ‚àÄ {R S T : Type u} [CommRing R] [CommRing S] [CommRing T],
      ‚àÄ (f : R ‚Üí+* S) (g : S ‚Üí+* T), P (g.comp f) ‚Üí P g)
    {X Y Z : Scheme.{u}} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z} (h : affineLocally (@P) (f ‚â´ g)) :
    affineLocally (@P) f := by
  let ùí∞ : ‚àÄ i, ((Z.affineCover.pullbackCover (f ‚â´ g)).obj i).OpenCover := by
    intro i
    refine Scheme.OpenCover.bind ?_ fun i => Scheme.affineCover _
    apply Scheme.OpenCover.pushforwardIso _
      (pullbackRightPullbackFstIso g (Z.affineCover.map i) f).hom
    apply Scheme.Pullback.openCoverOfRight
    exact (pullback g (Z.affineCover.map i)).affineCover
  have hùí∞ : ‚àÄ i j, IsAffine ((ùí∞ i).obj j) := by dsimp [ùí∞]; infer_instance
  let ùí∞' := (Z.affineCover.pullbackCover g).bind fun i => Scheme.affineCover _
  have hùí∞' : ‚àÄ i, IsAffine (ùí∞'.obj i) := by dsimp [ùí∞']; infer_instance
  rw [hP.affine_openCover_iff f ùí∞' fun i => Scheme.affineCover _]
  rw [hP.affine_openCover_iff (f ‚â´ g) Z.affineCover ùí∞] at h
  rintro ‚ü®i, j‚ü© k
  dsimp at i j k
  specialize h i ‚ü®j, k‚ü©
  dsimp only [ùí∞, ùí∞', Scheme.OpenCover.bind_map, Scheme.OpenCover.pushforwardIso_obj,
    Scheme.Pullback.openCoverOfRight_obj, Scheme.OpenCover.pushforwardIso_map,
    Scheme.Pullback.openCoverOfRight_map, Scheme.OpenCover.bind_obj,
    Scheme.OpenCover.pullbackCover_obj, Scheme.OpenCover.pullbackCover_map] at h ‚ä¢
  rw [Category.assoc, Category.assoc, pullbackRightPullbackFstIso_hom_snd,
    pullback.lift_snd_assoc, Category.assoc, ‚Üê Category.assoc, op_comp, Functor.map_comp] at h
  exact H _ _ h

theorem affineLocally_isStableUnderComposition : (affineLocally @P).IsStableUnderComposition where
  comp_mem {X Y S} f g hf hg := by
    let ùí∞ : ‚àÄ i, ((S.affineCover.pullbackCover (f ‚â´ g)).obj i).OpenCover := by
      intro i
      refine Scheme.OpenCover.bind ?_ fun i => Scheme.affineCover _
      apply Scheme.OpenCover.pushforwardIso _
        (pullbackRightPullbackFstIso g (S.affineCover.map i) f).hom
      apply Scheme.Pullback.openCoverOfRight
      exact (pullback g (S.affineCover.map i)).affineCover
    -- Porting note: used to be - rw [hP.affine_openCover_iff (f ‚â´ g) S.affineCover _] - but
    -- metavariables cause problems in the instance search
    apply (@affine_openCover_iff _ hP _ _ (f ‚â´ g) S.affineCover _ ?_ ?_).mpr
    rotate_left
    ¬∑ exact ùí∞
    ¬∑ intro i j; dsimp [ùí∞] at *; infer_instance
    ¬∑ rintro i ‚ü®j, k‚ü©
      dsimp at i j k
      dsimp only [ùí∞, Scheme.OpenCover.bind_map, Scheme.OpenCover.pushforwardIso_obj,
        Scheme.Pullback.openCoverOfRight_obj, Scheme.OpenCover.pushforwardIso_map,
        Scheme.Pullback.openCoverOfRight_map, Scheme.OpenCover.bind_obj]
      rw [Category.assoc, Category.assoc, pullbackRightPullbackFstIso_hom_snd,
        pullback.lift_snd_assoc, Category.assoc, ‚Üê Category.assoc, op_comp, Functor.map_comp]
      apply hP.StableUnderComposition
      ¬∑ -- Porting note: used to be exact _|>. hg i j but that can't find an instance
        apply hP.affine_openCover_iff _ _ _|>.mp
        exact hg
      ¬∑ letI := hP.hasAffinePropertyAffineLocally
        replace hf := HasAffineProperty.of_isPullback (.of_hasPullback _
          ((pullback g (S.affineCover.map i)).affineCover.map j ‚â´ pullback.fst _ _)) hf
        -- Porting note: again strange behavior of TFAE
        have := (hP.affine_openCover_TFAE
          (pullback.snd f ((pullback g (S.affineCover.map i)).affineCover.map j ‚â´
            pullback.fst _ _))).out 0 3
        apply this.mp hf

end RingHom.PropertyIsLocal
