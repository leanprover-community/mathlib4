/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.Basic
import Mathlib.RingTheory.LocalProperties

#align_import algebraic_geometry.morphisms.ring_hom_properties from "leanprover-community/mathlib"@"d39590fc8728fbf6743249802486f8c91ffe07bc"

/-!

# Properties of morphisms from properties of ring homs.

We provide the basic framework for talking about properties of morphisms that come from properties
of ring homs. For `P` a property of ring homs, we have two ways of defining a property of scheme
morphisms:

Let `f : X âŸ¶ Y`,
- `targetAffineLocally (affine_and P)`: the preimage of an affine open `U = Spec A` is affine
  (`= Spec B`) and `A âŸ¶ B` satisfies `P`. (TODO)
- `affineLocally P`: For each pair of affine open `U = Spec A âŠ† X` and `V = Spec B âŠ† f â»Â¹' U`,
  the ring hom `A âŸ¶ B` satisfies `P`.

For these notions to be well defined, we require `P` be a sufficient local property. For the former,
`P` should be local on the source (`RingHom.RespectsIso P`, `RingHom.LocalizationPreserves P`,
`RingHom.OfLocalizationSpan`), and `targetAffineLocally (affine_and P)` will be local on
the target. (TODO)

For the latter `P` should be local on the target (`RingHom.PropertyIsLocal P`), and
`affineLocally P` will be local on both the source and the target.

Further more, these properties are stable under compositions (resp. base change) if `P` is. (TODO)

-/

-- Explicit universe annotations were used in this file to improve perfomance #12737

universe u

open CategoryTheory Opposite TopologicalSpace CategoryTheory.Limits AlgebraicGeometry

variable (P : âˆ€ {R S : Type u} [CommRing R] [CommRing S], (R â†’+* S) â†’ Prop)

namespace RingHom

variable {P}
theorem RespectsIso.basicOpen_iff (hP : RespectsIso @P) {X Y : Scheme.{u}} [IsAffine X] [IsAffine Y]
    (f : X âŸ¶ Y) (r : Y.presheaf.obj (Opposite.op âŠ¤)) :
    P (Scheme.Î“.map (f âˆ£_ Y.basicOpen r).op) â†”
    P (@IsLocalization.Away.map (Y.presheaf.obj (Opposite.op âŠ¤)) _
      (Y.presheaf.obj (Opposite.op <| Y.basicOpen r)) _ _ (X.presheaf.obj (Opposite.op âŠ¤)) _
      (X.presheaf.obj (Opposite.op <| X.basicOpen (Scheme.Î“.map f.op r))) _ _
      (Scheme.Î“.map f.op) r _ <| @isLocalization_away_of_isAffine X _ (Scheme.Î“.map f.op r)) := by
  rw [Î“_map_morphismRestrict, hP.cancel_left_isIso, hP.cancel_right_isIso,
    â† hP.cancel_right_isIso (f.val.c.app (Opposite.op (Y.basicOpen r)))
      (X.presheaf.map (eqToHom (Scheme.preimage_basicOpen f r).symm).op), â† eq_iff_iff]
  congr
  delta IsLocalization.Away.map
  refine IsLocalization.ringHom_ext (Submonoid.powers r) ?_
  generalize_proofs
  haveI i1 := @isLocalization_away_of_isAffine X _ (Scheme.Î“.map f.op r)
  -- Porting note: needs to be very explicit here
  convert
    (@IsLocalization.map_comp (hy := â€¹_ â‰¤ _â€º) (Y.presheaf.obj <| Opposite.op (Scheme.basicOpen Y r))
    _ _ (isLocalization_away_of_isAffine _) _ _ _ i1).symm using 1
  change Y.presheaf.map _ â‰« _ = _ â‰« X.presheaf.map _
  rw [f.val.c.naturality_assoc]
  simp only [TopCat.Presheaf.pushforwardObj_map, â† X.presheaf.map_comp]
  congr 1
#align ring_hom.respects_iso.basic_open_iff RingHom.RespectsIso.basicOpen_iff

theorem RespectsIso.basicOpen_iff_localization (hP : RespectsIso @P) {X Y : Scheme.{u}} [IsAffine X]
    [IsAffine Y] (f : X âŸ¶ Y) (r : Y.presheaf.obj (Opposite.op âŠ¤)) :
    P (Scheme.Î“.map (f âˆ£_ Y.basicOpen r).op) â†” P (Localization.awayMap (Scheme.Î“.map f.op) r) := by
  refine (hP.basicOpen_iff _ _).trans ?_
  -- Porting note: was a one line term mode proof, but this `dsimp` is vital so the term mode
  -- one liner is not possible
  dsimp
  rw [â† hP.is_localization_away_iff]
#align ring_hom.respects_iso.basic_open_iff_localization RingHom.RespectsIso.basicOpen_iff_localization

@[deprecated (since := "2024-03-02")] alias
RespectsIso.ofRestrict_morphismRestrict_iff_of_isAffine := RespectsIso.basicOpen_iff_localization

theorem RespectsIso.ofRestrict_morphismRestrict_iff (hP : RingHom.RespectsIso @P) {X Y : Scheme.{u}}
    [IsAffine Y] (f : X âŸ¶ Y) (r : Y.presheaf.obj (Opposite.op âŠ¤)) (U : Opens X.carrier)
    (hU : IsAffineOpen U) {V : Opens _}
    (e : V = (Scheme.Î¹Opens <| f â»Â¹áµ Y.basicOpen r) â»Â¹áµ U) :
    P (Scheme.Î“.map (Scheme.Î¹Opens V â‰« f âˆ£_ Y.basicOpen r).op) â†”
    P (Localization.awayMap (Scheme.Î“.map (Scheme.Î¹Opens U â‰« f).op) r) := by
  subst e
  refine (hP.cancel_right_isIso _
    (Scheme.Î“.mapIso (Scheme.restrictRestrictComm _ _ _).op).inv).symm.trans ?_
  haveI : IsAffine _ := hU
  rw [â† hP.basicOpen_iff_localization, iff_iff_eq]
  congr 1
  simp only [Functor.mapIso_inv, Iso.op_inv, â† Functor.map_comp, â† op_comp, morphismRestrict_comp]
  rw [â† Category.assoc]
  congr 3
  rw [â† cancel_mono (Scheme.Î¹Opens _), Category.assoc, Scheme.restrictRestrictComm,
    IsOpenImmersion.isoOfRangeEq_inv_fac, morphismRestrict_Î¹]
#align ring_hom.respects_iso.of_restrict_morphism_restrict_iff RingHom.RespectsIso.ofRestrict_morphismRestrict_iff

theorem StableUnderBaseChange.Î“_pullback_fst (hP : StableUnderBaseChange @P) (hP' : RespectsIso @P)
    {X Y S : Scheme} [IsAffine X] [IsAffine Y] [IsAffine S] (f : X âŸ¶ S) (g : Y âŸ¶ S)
    (H : P (Scheme.Î“.map g.op)) : P (Scheme.Î“.map (pullback.fst : pullback f g âŸ¶ _).op) := by
  -- Porting note (#11224): change `rw` to `erw`
  erw [â† PreservesPullback.iso_inv_fst AffineScheme.forgetToScheme (AffineScheme.ofHom f)
      (AffineScheme.ofHom g)]
  rw [op_comp, Functor.map_comp, hP'.cancel_right_isIso, AffineScheme.forgetToScheme_map]
  have :=
    _root_.congr_arg Quiver.Hom.unop
      (PreservesPullback.iso_hom_fst AffineScheme.Î“.rightOp (AffineScheme.ofHom f)
        (AffineScheme.ofHom g))
  simp only [Quiver.Hom.unop_op, Functor.rightOp_map, unop_comp] at this
  delta AffineScheme.Î“ at this
  simp only [Quiver.Hom.unop_op, Functor.comp_map, AffineScheme.forgetToScheme_map,
    Functor.op_map] at this
  rw [â† this, hP'.cancel_right_isIso,
    â† pushoutIsoUnopPullback_inl_hom (Quiver.Hom.unop _) (Quiver.Hom.unop _),
    hP'.cancel_right_isIso]
  exact hP.pushout_inl _ hP' _ _ H
#align ring_hom.stable_under_base_change.Î“_pullback_fst RingHom.StableUnderBaseChange.Î“_pullback_fst

end RingHom

namespace AlgebraicGeometry

/-- For `P` a property of ring homomorphisms, `sourceAffineLocally P` holds for `f : X âŸ¶ Y`
whenever `P` holds for the restriction of `f` on every affine open subset of `X`. -/
def sourceAffineLocally : AffineTargetMorphismProperty := fun X _ f _ =>
  âˆ€ U : X.affineOpens, P (Scheme.Î“.map (X.ofRestrict U.1.openEmbedding â‰« f).op)
#align algebraic_geometry.source_affine_locally AlgebraicGeometry.sourceAffineLocally

/-- For `P` a property of ring homomorphisms, `affineLocally P` holds for `f : X âŸ¶ Y` if for each
affine open `U = Spec A âŠ† Y` and `V = Spec B âŠ† f â»Â¹' U`, the ring hom `A âŸ¶ B` satisfies `P`.
Also see `affineLocally_iff_affineOpens_le`. -/
abbrev affineLocally : MorphismProperty Scheme.{u} :=
  targetAffineLocally (sourceAffineLocally @P)
#align algebraic_geometry.affine_locally AlgebraicGeometry.affineLocally

variable {P}

theorem sourceAffineLocally_respectsIso (hâ‚ : RingHom.RespectsIso @P) :
    (sourceAffineLocally @P).toProperty.RespectsIso := by
  apply AffineTargetMorphismProperty.respectsIso_mk
  Â· introv H U
    rw [â† hâ‚.cancel_right_isIso _ (Scheme.Î“.map (Scheme.restrictMapIso e.inv U.1).hom.op), â†
      Functor.map_comp, â† op_comp]
    convert H âŸ¨_, U.prop.map_isIso e.invâŸ© using 3
    rw [IsOpenImmersion.isoOfRangeEq_hom_fac_assoc, Category.assoc,
      e.inv_hom_id_assoc]
  Â· introv H U
    rw [â† Category.assoc, op_comp, Functor.map_comp, hâ‚.cancel_left_isIso]
    exact H U
#align algebraic_geometry.source_affine_locally_respects_iso AlgebraicGeometry.sourceAffineLocally_respectsIso

theorem affineLocally_respectsIso (h : RingHom.RespectsIso @P) : (affineLocally @P).RespectsIso :=
  targetAffineLocally_respectsIso (sourceAffineLocally_respectsIso h)
#align algebraic_geometry.affine_locally_respects_iso AlgebraicGeometry.affineLocally_respectsIso

theorem affineLocally_iff_affineOpens_le
    (hP : RingHom.RespectsIso @P) {X Y : Scheme.{u}} (f : X âŸ¶ Y) :
    affineLocally.{u} (@P) f â†”
    âˆ€ (U : Y.affineOpens) (V : X.affineOpens) (e : V.1 â‰¤ (Opens.map f.1.base).obj U.1),
      P (Scheme.Hom.appLe f e) := by
  apply forall_congr'
  intro U
  delta sourceAffineLocally
  simp_rw [op_comp, Scheme.Î“.map_comp, Î“_map_morphismRestrict, Category.assoc, Scheme.Î“_map_op,
    hP.cancel_left_isIso (Y.presheaf.map (eqToHom _).op)]
  constructor
  Â· intro H V e
    let U' := (Opens.map f.val.base).obj U.1
    have e'' : (Scheme.Hom.opensFunctor (X.ofRestrict U'.openEmbedding)).obj
        (X.ofRestrict U'.openEmbeddingâ»Â¹áµ V) = V := by
      ext1; refine Set.image_preimage_eq_inter_range.trans (Set.inter_eq_left.mpr ?_)
      erw [Subtype.range_val]
      exact e
    have h : X.ofRestrict U'.openEmbedding â»Â¹áµ â†‘V âˆˆ Scheme.affineOpens (X.restrict _) := by
      apply (X.ofRestrict U'.openEmbedding).isAffineOpen_iff_of_isOpenImmersion.mp
      -- Porting note: was convert V.2
      rw [e'']
      convert V.2
    have := H âŸ¨(Opens.map (X.ofRestrict U'.openEmbedding).1.base).obj V.1, hâŸ©
    rw [â† hP.cancel_right_isIso _ (X.presheaf.map (eqToHom _)), Category.assoc,
      â† X.presheaf.map_comp]
    Â· dsimp; convert this using 1
      congr 1
      rw [X.presheaf.map_comp]
      Â· simp only [Scheme.ofRestrict_val_c_app, Scheme.restrict_presheaf_map, â† X.presheaf.map_comp]
        congr 1
    Â· dsimp only [Functor.op, unop_op]
      rw [Opens.openEmbedding_obj_top]
      congr 1
      exact e''.symm
  Â· intro H V
    specialize H âŸ¨_, V.2.image_of_isOpenImmersion (X.ofRestrict _)âŸ© (Subtype.coe_image_subset _ _)
    rw [â† hP.cancel_right_isIso _ (X.presheaf.map (eqToHom _)), Category.assoc]
    Â· convert H
      simp only [Scheme.ofRestrict_val_c_app, Scheme.restrict_presheaf_map, â† X.presheaf.map_comp]
      congr 1
    Â· dsimp only [Functor.op, unop_op]; rw [Opens.openEmbedding_obj_top]
#align algebraic_geometry.affine_locally_iff_affine_opens_le AlgebraicGeometry.affineLocally_iff_affineOpens_le

theorem scheme_restrict_basicOpen_of_localizationPreserves (hâ‚ : RingHom.RespectsIso @P)
    (hâ‚‚ : RingHom.LocalizationPreserves @P) {X Y : Scheme.{u}} [IsAffine Y] (f : X âŸ¶ Y)
    (r : Y.presheaf.obj (op âŠ¤)) (H : sourceAffineLocally (@P) f)
    (U : (X.restrict ((Opens.map f.1.base).obj <| Y.basicOpen r).openEmbedding).affineOpens) :
    P (Scheme.Î“.map ((X.restrict ((Opens.map f.1.base).obj <|
      Y.basicOpen r).openEmbedding).ofRestrict U.1.openEmbedding â‰« f âˆ£_ Y.basicOpen r).op) := by
  specialize H âŸ¨_, U.2.image_of_isOpenImmersion (X.ofRestrict _)âŸ©
  letI i1 : Algebra (Y.presheaf.obj <| Opposite.op âŠ¤) (Localization.Away r) := Localization.algebra
  exact (hâ‚.ofRestrict_morphismRestrict_iff f r
    ((Scheme.Hom.opensFunctor
      (X.ofRestrict ((Opens.map f.1.base).obj <| Y.basicOpen r).openEmbedding)).obj U.1)
    (IsAffineOpen.image_of_isOpenImmersion U.2
      (X.ofRestrict ((Opens.map f.1.base).obj <| Y.basicOpen r).openEmbedding))
    (Opens.ext (Set.preimage_image_eq _ Subtype.coe_injective).symm)).mpr (hâ‚‚.away r H)
set_option linter.uppercaseLean3 false in
#align algebraic_geometry.Scheme_restrict_basic_open_of_localization_preserves AlgebraicGeometry.scheme_restrict_basicOpen_of_localizationPreserves

theorem sourceAffineLocally_isLocal (hâ‚ : RingHom.RespectsIso @P)
    (hâ‚‚ : RingHom.LocalizationPreserves @P) (hâ‚ƒ : RingHom.OfLocalizationSpan @P) :
    (sourceAffineLocally @P).IsLocal := by
  constructor
  Â· exact sourceAffineLocally_respectsIso hâ‚
  Â· introv H U
    apply scheme_restrict_basicOpen_of_localizationPreserves hâ‚ hâ‚‚; assumption
  Â· introv hs hs' U
    apply hâ‚ƒ _ _ hs
    intro r
    have := hs' r âŸ¨(Opens.map (X.ofRestrict _).1.base).obj U.1, ?_âŸ©
    Â· rwa [hâ‚.ofRestrict_morphismRestrict_iff] at this
      Â· exact U.2
      Â· rfl
    Â· suffices âˆ€ (V) (_ : V = (Opens.map f.val.base).obj (Y.basicOpen r.val)),
          IsAffineOpen ((Opens.map (X.ofRestrict V.openEmbedding).1.base).obj U.1) by
        exact this _ rfl
      intro V hV
      rw [Scheme.preimage_basicOpen] at hV
      subst hV
      exact U.2.Î¹Opens_preimage (Scheme.Î“.map f.op r.1)
#align algebraic_geometry.source_affine_locally_is_local AlgebraicGeometry.sourceAffineLocally_isLocal

variable (hP : RingHom.PropertyIsLocal @P)

theorem sourceAffineLocally_of_source_open_cover_aux (hâ‚ : RingHom.RespectsIso @P)
    (hâ‚ƒ : RingHom.OfLocalizationSpanTarget @P) {X Y : Scheme.{u}} (f : X âŸ¶ Y) (U : X.affineOpens)
    (s : Set (X.presheaf.obj (op U.1))) (hs : Ideal.span s = âŠ¤)
    (hs' : âˆ€ r : s, P (Scheme.Î“.map (Scheme.Î¹Opens (X.basicOpen r.1) â‰« f).op)) :
    P (Scheme.Î“.map (Scheme.Î¹Opens U â‰« f).op) := by
  apply_fun Ideal.map (X.presheaf.map (eqToHom U.1.openEmbedding_obj_top).op) at hs
  rw [Ideal.map_span, Ideal.map_top] at hs
  apply hâ‚ƒ.ofIsLocalization hâ‚ _ _ hs
  rintro âŸ¨s, r, hr, hsâŸ©
  refine âŸ¨_, _, _, @AlgebraicGeometry.Î“_restrict_isLocalization (X âˆ£_áµ¤ U.1) U.2 s, ?_âŸ©
  rw [RingHom.algebraMap_toAlgebra, â† CommRingCat.comp_eq_ring_hom_comp, â† Functor.map_comp,
    â† op_comp, â† hâ‚.cancel_right_isIso _ (Scheme.Î“.mapIso (Scheme.restrictRestrict _ _ _).op).inv]
  subst hs
  rw [â† hâ‚.cancel_right_isIso _
    (Scheme.Î“.mapIso (Scheme.restrictIsoOfEq _ (Scheme.map_basicOpen_map _ _ _)).op).inv]
  simp only [Functor.mapIso_inv, Iso.op_inv, â† Functor.map_comp, â† op_comp,
    Scheme.restrictRestrict_inv_restrict_restrict_assoc, Scheme.restrictIsoOfEq,
    IsOpenImmersion.isoOfRangeEq_inv_fac_assoc]
  exact hs' âŸ¨r, hrâŸ©
#align algebraic_geometry.source_affine_locally_of_source_open_cover_aux AlgebraicGeometry.sourceAffineLocally_of_source_open_cover_aux

theorem isOpenImmersionCat_comp_of_sourceAffineLocally (hâ‚ : RingHom.RespectsIso @P)
    {X Y Z : Scheme.{u}} [IsAffine X] [IsAffine Z] (f : X âŸ¶ Y) [IsOpenImmersion f] (g : Y âŸ¶ Z)
    (hâ‚‚ : sourceAffineLocally (@P) g) : P (Scheme.Î“.map (f â‰« g).op) := by
  rw [â† hâ‚.cancel_right_isIso _
    (Scheme.Î“.map (IsOpenImmersion.isoOfRangeEq (Y.ofRestrict _) f _).hom.op),
    â† Functor.map_comp, â† op_comp]
  Â· convert hâ‚‚ âŸ¨_, isAffineOpen_opensRange fâŸ© using 3
    Â· rw [IsOpenImmersion.isoOfRangeEq_hom_fac_assoc]
      exact Subtype.range_coe
#align algebraic_geometry.is_open_immersion_comp_of_source_affine_locally AlgebraicGeometry.isOpenImmersionCat_comp_of_sourceAffineLocally

end AlgebraicGeometry

open AlgebraicGeometry

namespace RingHom.PropertyIsLocal

variable {P} (hP : RingHom.PropertyIsLocal @P)

theorem sourceAffineLocally_of_source_openCover {X Y : Scheme.{u}} (f : X âŸ¶ Y) [IsAffine Y]
    (ğ’° : X.OpenCover) [âˆ€ i, IsAffine (ğ’°.obj i)] (H : âˆ€ i, P (Scheme.Î“.map (ğ’°.map i â‰« f).op)) :
    sourceAffineLocally (@P) f := by
  let S i := (âŸ¨âŸ¨Set.range (ğ’°.map i).1.base, (ğ’°.IsOpen i).base_open.isOpen_rangeâŸ©,
    isAffineOpen_opensRange (ğ’°.map i)âŸ© : X.affineOpens)
  intro U
  -- Porting note: here is what we are eliminating into Lean
  apply of_affine_open_cover
    (P := fun V => P (Scheme.Î“.map (X.ofRestrict (Opens.openEmbedding V.val) â‰« f).op)) U
  pick_goal 5
  Â· exact Set.range S
  Â· intro U r H
    -- Porting note: failing on instance synthesis for an (unspecified) meta variable
    -- made Ï† explicit and forced to use dsimp in the proof
    convert hP.StableUnderComposition
      (S := Scheme.Î“.obj (Opposite.op (X.restrict <| Opens.openEmbedding U.val)))
      (T := Scheme.Î“.obj (Opposite.op (X.restrict <| Opens.openEmbedding (X.basicOpen r))))
      ?_ ?_ H ?_ using 1
    swap
    Â· refine X.presheaf.map
          (@homOfLE _ _ ((IsOpenMap.functor _).obj _) ((IsOpenMap.functor _).obj _) ?_).op
      dsimp
      rw [Opens.openEmbedding_obj_top, Opens.openEmbedding_obj_top]
      exact X.basicOpen_le _
    Â· rw [op_comp, op_comp, Functor.map_comp, Functor.map_comp]
      refine (Eq.trans ?_ (Category.assoc (obj := CommRingCat) _ _ _).symm : _)
      congr 1
      dsimp
      refine Eq.trans ?_ (X.presheaf.map_comp _ _)
      change X.presheaf.map _ = _
      congr!
    -- Porting note: need to pass Algebra through explicitly
    convert @HoldsForLocalizationAway _ hP _
      (Scheme.Î“.obj (Opposite.op (X.restrict (X.basicOpen r).openEmbedding))) _ _ ?_
      (X.presheaf.map (eqToHom U.1.openEmbedding_obj_top).op r) ?_
    Â· exact RingHom.algebraMap_toAlgebra
        (R := Scheme.Î“.obj <| Opposite.op <| X.restrict (U.1.openEmbedding))
        (S :=
          Scheme.Î“.obj (Opposite.op <| X.restrict (X.affineBasicOpen r).1.openEmbedding)) _|>.symm
    Â· dsimp [Scheme.Î“]
      have := U.2
      rw [â† U.1.openEmbedding_obj_top] at this
      -- Porting note: the second argument of `IsLocalization.Away` is a type, and we want
      -- to generate an equality, so using `typeEqs := true` to force allowing type equalities.
      convert (config := {typeEqs := true, transparency := .default})
          this.isLocalization_basicOpen _ using 5
      all_goals rw [Opens.openEmbedding_obj_top]; exact (Scheme.basicOpen_res_eq _ _ _).symm
  Â· introv hs hs'
    exact sourceAffineLocally_of_source_open_cover_aux hP.respectsIso hP.2 _ _ _ hs hs'
  Â· rw [Set.eq_univ_iff_forall]
    intro x
    rw [Set.mem_iUnion]
    exact âŸ¨âŸ¨_, ğ’°.f x, rflâŸ©, ğ’°.Covers xâŸ©
  Â· rintro âŸ¨_, i, rflâŸ©
    specialize H i
    rw [â† hP.respectsIso.cancel_right_isIso _
        (Scheme.Î“.map
          (IsOpenImmersion.isoOfRangeEq (ğ’°.map i) (X.ofRestrict (S i).1.openEmbedding)
                Subtype.range_coe.symm).inv.op)] at H
    rwa [â† Scheme.Î“.map_comp, â† op_comp, IsOpenImmersion.isoOfRangeEq_inv_fac_assoc] at H
#align ring_hom.property_is_local.source_affine_locally_of_source_open_cover RingHom.PropertyIsLocal.sourceAffineLocally_of_source_openCover

theorem affine_openCover_TFAE {X Y : Scheme.{u}} [IsAffine Y] (f : X âŸ¶ Y) :
    List.TFAE
      [sourceAffineLocally (@P) f,
        âˆƒ (ğ’° : Scheme.OpenCover.{u} X) (_ : âˆ€ i, IsAffine (ğ’°.obj i)),
          âˆ€ i : ğ’°.J, P (Scheme.Î“.map (ğ’°.map i â‰« f).op),
        âˆ€ (ğ’° : Scheme.OpenCover.{u} X) [âˆ€ i, IsAffine (ğ’°.obj i)] (i : ğ’°.J),
          P (Scheme.Î“.map (ğ’°.map i â‰« f).op),
        âˆ€ {U : Scheme} (g : U âŸ¶ X) [IsAffine U] [IsOpenImmersion g],
          P (Scheme.Î“.map (g â‰« f).op)] := by
  tfae_have 1 â†’ 4
  Â· intro H U g _ hg
    specialize H âŸ¨âŸ¨_, hg.base_open.isOpen_rangeâŸ©, isAffineOpen_opensRange gâŸ©
    rw [â† hP.respectsIso.cancel_right_isIso _ (Scheme.Î“.map (IsOpenImmersion.isoOfRangeEq g
      (X.ofRestrict (Opens.openEmbedding âŸ¨_, hg.base_open.isOpen_rangeâŸ©))
      Subtype.range_coe.symm).hom.op),
      â† Scheme.Î“.map_comp, â† op_comp, IsOpenImmersion.isoOfRangeEq_hom_fac_assoc] at H
    exact H
  tfae_have 4 â†’ 3
  Â· intro H ğ’° _ i; apply H
  tfae_have 3 â†’ 2
  Â· intro H; exact âŸ¨X.affineCover, inferInstance, H _âŸ©
  tfae_have 2 â†’ 1
  Â· rintro âŸ¨ğ’°, _, hğ’°âŸ©
    exact sourceAffineLocally_of_source_openCover hP f ğ’° hğ’°
  tfae_finish
#align ring_hom.property_is_local.affine_open_cover_tfae RingHom.PropertyIsLocal.affine_openCover_TFAE

theorem openCover_TFAE {X Y : Scheme.{u}} [IsAffine Y] (f : X âŸ¶ Y) :
    List.TFAE
      [sourceAffineLocally (@P) f,
        âˆƒ ğ’° : Scheme.OpenCover.{u} X, âˆ€ i : ğ’°.J, sourceAffineLocally (@P) (ğ’°.map i â‰« f),
        âˆ€ (ğ’° : Scheme.OpenCover.{u} X) (i : ğ’°.J), sourceAffineLocally (@P) (ğ’°.map i â‰« f),
        âˆ€ {U : Scheme} (g : U âŸ¶ X) [IsOpenImmersion g], sourceAffineLocally (@P) (g â‰« f)] := by
  tfae_have 1 â†’ 4
  Â· intro H U g hg V
    -- Porting note: this has metavariable if I put it directly into rw
    have := (hP.affine_openCover_TFAE f).out 0 3
    rw [this] at H
    haveI : IsAffine _ := V.2
    rw [â† Category.assoc]
    -- Porting note: Lean could find this previously
    have : IsOpenImmersion <| (Scheme.ofRestrict U (Opens.openEmbedding V.val)) â‰« g :=
      LocallyRingedSpace.IsOpenImmersion.comp _ _
    apply H
  tfae_have 4 â†’ 3
  Â· intro H ğ’° _ i; apply H
  tfae_have 3 â†’ 2
  Â· intro H; exact âŸ¨X.affineCover, H _âŸ©
  tfae_have 2 â†’ 1
  Â· rintro âŸ¨ğ’°, hğ’°âŸ©
    -- Porting note: this has metavariable if I put it directly into rw
    have := (hP.affine_openCover_TFAE f).out 0 1
    rw [this]
    refine âŸ¨ğ’°.bind fun _ => Scheme.affineCover _, ?_, ?_âŸ©
    Â· intro i; dsimp; infer_instance
    Â· intro i
      specialize hğ’° i.1
      -- Porting note: this has metavariable if I put it directly into rw
      have := (hP.affine_openCover_TFAE (ğ’°.map i.fst â‰« f)).out 0 3
      rw [this] at hğ’°
      -- Porting note: this was discharged after the apply previously
      have : IsAffine (Scheme.OpenCover.obj
        (Scheme.OpenCover.bind ğ’° fun x â†¦ Scheme.affineCover (Scheme.OpenCover.obj ğ’° x)) i) := by
          dsimp; infer_instance
      apply @hğ’° _ (show _ from _)
  tfae_finish
#align ring_hom.property_is_local.open_cover_tfae RingHom.PropertyIsLocal.openCover_TFAE

theorem sourceAffineLocally_comp_of_isOpenImmersion {X Y Z : Scheme.{u}} [IsAffine Z] (f : X âŸ¶ Y)
    (g : Y âŸ¶ Z) [IsOpenImmersion f] (H : sourceAffineLocally (@P) g) :
    sourceAffineLocally (@P) (f â‰« g) := by
      -- Porting note: more tfae mis-behavior
      have := (hP.openCover_TFAE g).out 0 3
      apply this.mp H
#align ring_hom.property_is_local.source_affine_locally_comp_of_is_open_immersion RingHom.PropertyIsLocal.sourceAffineLocally_comp_of_isOpenImmersion

theorem source_affine_openCover_iff {X Y : Scheme.{u}} (f : X âŸ¶ Y) [IsAffine Y]
    (ğ’° : Scheme.OpenCover.{u} X) [âˆ€ i, IsAffine (ğ’°.obj i)] :
    sourceAffineLocally (@P) f â†” âˆ€ i, P (Scheme.Î“.map (ğ’°.map i â‰« f).op) := by
  -- Porting note: seems like TFAE is misbehaving; this used to be pure term proof but
  -- had strange failures where the output of TFAE turned into a metavariable when used despite
  -- being correctly displayed in the infoview
  refine âŸ¨fun H => ?_, fun H => ?_âŸ©
  Â· have h := (hP.affine_openCover_TFAE f).out 0 2
    apply h.mp
    exact H
  Â· have h := (hP.affine_openCover_TFAE f).out 1 0
    apply h.mp
    use ğ’°
#align ring_hom.property_is_local.source_affine_open_cover_iff RingHom.PropertyIsLocal.source_affine_openCover_iff

theorem isLocal_sourceAffineLocally : (sourceAffineLocally @P).IsLocal :=
  sourceAffineLocally_isLocal hP.respectsIso hP.LocalizationPreserves
    (@RingHom.PropertyIsLocal.ofLocalizationSpan _ hP)
#align ring_hom.property_is_local.is_local_source_affine_locally RingHom.PropertyIsLocal.isLocal_sourceAffineLocally

theorem is_local_affineLocally : PropertyIsLocalAtTarget (affineLocally @P) :=
  hP.isLocal_sourceAffineLocally.targetAffineLocally_isLocal
#align ring_hom.property_is_local.is_local_affine_locally RingHom.PropertyIsLocal.is_local_affineLocally

theorem affine_openCover_iff {X Y : Scheme.{u}} (f : X âŸ¶ Y) (ğ’° : Scheme.OpenCover.{u} Y)
    [âˆ€ i, IsAffine (ğ’°.obj i)] (ğ’°' : âˆ€ i, Scheme.OpenCover.{u} ((ğ’°.pullbackCover f).obj i))
    [âˆ€ i j, IsAffine ((ğ’°' i).obj j)] :
    affineLocally (@P) f â†” âˆ€ i j, P (Scheme.Î“.map ((ğ’°' i).map j â‰« pullback.snd).op) :=
  (hP.isLocal_sourceAffineLocally.affine_openCover_iff f ğ’°).trans
    (forall_congr' fun i => hP.source_affine_openCover_iff _ (ğ’°' i))
#align ring_hom.property_is_local.affine_open_cover_iff RingHom.PropertyIsLocal.affine_openCover_iff

theorem source_openCover_iff {X Y : Scheme.{u}} (f : X âŸ¶ Y) (ğ’° : Scheme.OpenCover.{u} X) :
    affineLocally (@P) f â†” âˆ€ i, affineLocally (@P) (ğ’°.map i â‰« f) := by
  constructor
  Â· intro H i U
    rw [morphismRestrict_comp]
    apply hP.sourceAffineLocally_comp_of_isOpenImmersion
    apply H
  Â· intro H U
    haveI : IsAffine _ := U.2
    apply ((hP.openCover_TFAE (f âˆ£_ U.1)).out 1 0).mp
    use ğ’°.pullbackCover (X.ofRestrict _)
    intro i
    specialize H i U
    rw [morphismRestrict_comp] at H
    delta morphismRestrict at H
    have := sourceAffineLocally_respectsIso hP.respectsIso
    rw [Category.assoc, affine_cancel_left_isIso this, â†
      affine_cancel_left_isIso this (pullbackSymmetry _ _).hom,
      pullbackSymmetry_hom_comp_snd_assoc] at H
    exact H
#align ring_hom.property_is_local.source_open_cover_iff RingHom.PropertyIsLocal.source_openCover_iff

theorem affineLocally_of_isOpenImmersion (hP : RingHom.PropertyIsLocal @P) {X Y : Scheme.{u}}
    (f : X âŸ¶ Y) [hf : IsOpenImmersion f] : affineLocally (@P) f := by
  intro U
  haveI H : IsAffine _ := U.2
  rw [â† Category.comp_id (f âˆ£_ U)]
  apply hP.sourceAffineLocally_comp_of_isOpenImmersion
  -- Porting note: need to excuse Lean from synthesizing an instance
  rw [@source_affine_openCover_iff _ hP _ _ _ _ (Scheme.openCoverOfIsIso (ğŸ™ _)) (_)]
  Â· intro i
    let esto := Scheme.Î“.obj (Opposite.op (Y.restrict <| Opens.openEmbedding U.val))
    let eso := Scheme.Î“.obj (Opposite.op ((Scheme.openCoverOfIsIso
      (ğŸ™ (Y.restrict <| Opens.openEmbedding U.val))).obj i))
    -- Porting note: Lean this needed this spelled out before
    -- convert hP.HoldsAwayLocalizationAway _ (1 : Scheme.Î“.obj _) _
    have : ğŸ™ (Scheme.Î“.obj (Opposite.op (Y.restrict <| Opens.openEmbedding U.val)))
      = @algebraMap esto eso _ _ (_) := (RingHom.algebraMap_toAlgebra _).symm
    simp only [Scheme.Î“_obj, unop_op, Scheme.restrict_presheaf_obj, Scheme.openCoverOfIsIso_obj,
      Scheme.openCoverOfIsIso_map, Category.comp_id, op_id, Scheme.Î“_map, unop_id,
      Scheme.id_val_base, Scheme.id_app, eqToHom_refl, Scheme.restrict_presheaf_map,
      CategoryTheory.Functor.map_id] at *
    rw [this]
    have := hP.HoldsForLocalizationAway
    convert @this esto eso _ _ ?_ ?_ ?_
    Â· exact 1
    -- Porting note: again we have to bypass TC synthesis to keep Lean from running away
    Â· exact
        @IsLocalization.away_of_isUnit_of_bijective _ _ _ _ (_) _ isUnit_one Function.bijective_id
  Â· intro; exact H
#align ring_hom.property_is_local.affine_locally_of_is_open_immersion RingHom.PropertyIsLocal.affineLocally_of_isOpenImmersion

theorem affineLocally_of_comp
    (H : âˆ€ {R S T : Type u} [CommRing R] [CommRing S] [CommRing T],
      âˆ€ (f : R â†’+* S) (g : S â†’+* T), P (g.comp f) â†’ P g)
    {X Y Z : Scheme.{u}} {f : X âŸ¶ Y} {g : Y âŸ¶ Z} (h : affineLocally (@P) (f â‰« g)) :
    affineLocally (@P) f := by
  let ğ’° : âˆ€ i, ((Z.affineCover.pullbackCover (f â‰« g)).obj i).OpenCover := by
    intro i
    refine Scheme.OpenCover.bind ?_ fun i => Scheme.affineCover _
    apply Scheme.OpenCover.pushforwardIso _
      (pullbackRightPullbackFstIso g (Z.affineCover.map i) f).hom
    apply Scheme.Pullback.openCoverOfRight
    exact (pullback g (Z.affineCover.map i)).affineCover
  have hğ’° : âˆ€ i j, IsAffine ((ğ’° i).obj j) := by dsimp [ğ’°]; infer_instance
  let ğ’°' := (Z.affineCover.pullbackCover g).bind fun i => Scheme.affineCover _
  have hğ’°' : âˆ€ i, IsAffine (ğ’°'.obj i) := by dsimp [ğ’°']; infer_instance
  rw [hP.affine_openCover_iff f ğ’°' fun i => Scheme.affineCover _]
  rw [hP.affine_openCover_iff (f â‰« g) Z.affineCover ğ’°] at h
  rintro âŸ¨i, jâŸ© k
  dsimp at i j k
  specialize h i âŸ¨j, kâŸ©
  dsimp only [ğ’°, ğ’°', Scheme.OpenCover.bind_map, Scheme.OpenCover.pushforwardIso_obj,
    Scheme.Pullback.openCoverOfRight_obj, Scheme.OpenCover.pushforwardIso_map,
    Scheme.Pullback.openCoverOfRight_map, Scheme.OpenCover.bind_obj,
    Scheme.OpenCover.pullbackCover_obj, Scheme.OpenCover.pullbackCover_map] at h âŠ¢
  rw [Category.assoc, Category.assoc, pullbackRightPullbackFstIso_hom_snd,
    pullback.lift_snd_assoc, Category.assoc, â† Category.assoc, op_comp, Functor.map_comp] at h
  exact H _ _ h
#align ring_hom.property_is_local.affine_locally_of_comp RingHom.PropertyIsLocal.affineLocally_of_comp

theorem affineLocally_isStableUnderComposition : (affineLocally @P).IsStableUnderComposition where
  comp_mem {X Y S} f g hf hg := by
    let ğ’° : âˆ€ i, ((S.affineCover.pullbackCover (f â‰« g)).obj i).OpenCover := by
      intro i
      refine Scheme.OpenCover.bind ?_ fun i => Scheme.affineCover _
      apply Scheme.OpenCover.pushforwardIso _
        (pullbackRightPullbackFstIso g (S.affineCover.map i) f).hom
      apply Scheme.Pullback.openCoverOfRight
      exact (pullback g (S.affineCover.map i)).affineCover
    -- Porting note: used to be - rw [hP.affine_openCover_iff (f â‰« g) S.affineCover _] - but
    -- metavariables cause problems in the instance search
    apply (@affine_openCover_iff _ hP _ _ (f â‰« g) S.affineCover _ ?_ ?_).mpr
    rotate_left
    Â· exact ğ’°
    Â· intro i j; dsimp [ğ’°] at *; infer_instance
    Â· rintro i âŸ¨j, kâŸ©
      dsimp at i j k
      dsimp only [ğ’°, Scheme.OpenCover.bind_map, Scheme.OpenCover.pushforwardIso_obj,
        Scheme.Pullback.openCoverOfRight_obj, Scheme.OpenCover.pushforwardIso_map,
        Scheme.Pullback.openCoverOfRight_map, Scheme.OpenCover.bind_obj]
      rw [Category.assoc, Category.assoc, pullbackRightPullbackFstIso_hom_snd,
        pullback.lift_snd_assoc, Category.assoc, â† Category.assoc, op_comp, Functor.map_comp]
      apply hP.StableUnderComposition
      Â· -- Porting note: used to be exact _|>. hg i j but that can't find an instance
        apply hP.affine_openCover_iff _ _ _|>.mp
        exact hg
      Â· delta affineLocally at hf
        -- Porting note: again strange behavior of TFAE
        have := (hP.isLocal_sourceAffineLocally.affine_openCover_TFAE f).out 0 3
        rw [this] at hf
        -- Porting note: needed to help Lean with this instance (same as above)
        have : IsOpenImmersion <|
            ((pullback g (S.affineCover.map i)).affineCover.map j â‰« pullback.fst) :=
          LocallyRingedSpace.IsOpenImmersion.comp _ _
        specialize hf ((pullback g (S.affineCover.map i)).affineCover.map j â‰« pullback.fst)
        -- Porting note: again strange behavior of TFAE
        have := (hP.affine_openCover_TFAE
          (pullback.snd : pullback f ((pullback g (S.affineCover.map i)).affineCover.map j â‰«
          pullback.fst) âŸ¶ _)).out 0 3
        rw [this] at hf
        apply hf
#align ring_hom.property_is_local.affine_locally_stable_under_composition RingHom.PropertyIsLocal.affineLocally_isStableUnderComposition

end RingHom.PropertyIsLocal
