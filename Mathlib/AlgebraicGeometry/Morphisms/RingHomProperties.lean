/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.Basic
import Mathlib.RingTheory.LocalProperties

/-!

# Properties of morphisms from properties of ring homs.

We provide the basic framework for talking about properties of morphisms that come from properties
of ring homs. For `P` a property of ring homs, we have two ways of defining a property of scheme
morphisms:

Let `f : X âŸ¶ Y`,
- `targetAffineLocally (affine_and P)`: the preimage of an affine open `U = Spec A` is affine
  (`= Spec B`) and `A âŸ¶ B` satisfies `P`. (TODO)
- `affineLocally P`: For each pair of affine open `U = Spec A âŠ† X` and `V = Spec B âŠ† f â»Â¹' U`,
  the ring hom `A âŸ¶ B` satisfies `P`.

For these notions to be well defined, we require `P` be a sufficient local property. For the former,
`P` should be local on the source (`RingHom.RespectsIso P`, `RingHom.LocalizationPreserves P`,
`RingHom.OfLocalizationSpan`), and `targetAffineLocally (affine_and P)` will be local on
the target. (TODO)

For the latter `P` should be local on the target (`RingHom.PropertyIsLocal P`), and
`affineLocally P` will be local on both the source and the target.
We also provide the following interface:

## `HasRingHomProperty`

`HasRingHomProperty P Q` is a type class asserting that `P` is local at the target and the source,
and for `f : Spec B âŸ¶ Spec A`, it is equivalent to the ring hom property `Q`.

For `HasRingHomProperty P Q` and `f : X âŸ¶ Y`, we provide these API lemmas:
- `AlgebraicGeometry.HasAffineProperty.iff_appLE`:
    `P f` if and only if `Q (f.appLE U V _)` for all affine `U : Opens Y` and `V : Opens X`.
- `AlgebraicGeometry.HasAffineProperty.iff_of_openCover`:
    If `Y` is affine, `P f â†” âˆ€ i, Q ((ğ’°.map i â‰« f).app âŠ¤)` for an affine open cover `ğ’°` of `X`.
- `AlgebraicGeometry.HasAffineProperty.iff_of_isAffine`:
    If `X` and `Y` are affine, then `P f â†” Q (f.app âŠ¤)`.
- `AlgebraicGeometry.HasAffineProperty.Spec_iff`:
    `P (Spec.map Ï†) â†” Q Ï†`
- `AlgebraicGeometry.HasAffineProperty.iff_of_iSup_eq_top`:
    If `Y` is affine, `P f â†” âˆ€ i, Q (f.appLE âŠ¤ (U i) _)` for a family `U` of affine opens of `X`.
- `AlgebraicGeometry.HasAffineProperty.of_isOpenImmersion`:
    If `f` is an open immersion then `P f`.
- `AlgebraicGeometry.HasAffineProperty.stableUnderBaseChange`:
    If `Q` is stable under base change, then so is `P`.

We also provide the instances `P.IsMultiplicative`, `P.IsStableUnderComposition`,
`IsLocalAtTarget P`, `IsLocalAtSource P`.

-/

-- Explicit universe annotations were used in this file to improve perfomance #12737

universe u

open CategoryTheory Opposite TopologicalSpace CategoryTheory.Limits AlgebraicGeometry

namespace RingHom

variable (P : âˆ€ {R S : Type u} [CommRing R] [CommRing S], (R â†’+* S) â†’ Prop)

theorem StableUnderBaseChange.pullback_fst_app_top
    (hP : StableUnderBaseChange P) (hP' : RespectsIso P)
    {X Y S : Scheme} [IsAffine X] [IsAffine Y] [IsAffine S] (f : X âŸ¶ S) (g : Y âŸ¶ S)
    (H : P (g.app âŠ¤)) : P ((pullback.fst f g).app âŠ¤) := by
  -- Porting note (#11224): change `rw` to `erw`
  erw [â† PreservesPullback.iso_inv_fst AffineScheme.forgetToScheme (AffineScheme.ofHom f)
      (AffineScheme.ofHom g)]
  rw [Scheme.comp_app, hP'.cancel_right_isIso, AffineScheme.forgetToScheme_map]
  have :=
    _root_.congr_arg Quiver.Hom.unop
      (PreservesPullback.iso_hom_fst AffineScheme.Î“.rightOp (AffineScheme.ofHom f)
        (AffineScheme.ofHom g))
  simp only [AffineScheme.Î“, Functor.rightOp_obj, Functor.comp_obj, Functor.op_obj, unop_comp,
    AffineScheme.forgetToScheme_obj, Scheme.Î“_obj, Functor.rightOp_map, Functor.comp_map,
    Functor.op_map, Quiver.Hom.unop_op, AffineScheme.forgetToScheme_map, Scheme.Î“_map] at this
  rw [â† this, hP'.cancel_right_isIso, â† pushoutIsoUnopPullback_inl_hom, hP'.cancel_right_isIso]
  exact hP.pushout_inl _ hP' _ _ H

end RingHom

namespace AlgebraicGeometry

section affineLocally

variable (P : âˆ€ {R S : Type u} [CommRing R] [CommRing S], (R â†’+* S) â†’ Prop)

/-- For `P` a property of ring homomorphisms, `sourceAffineLocally P` holds for `f : X âŸ¶ Y`
whenever `P` holds for the restriction of `f` on every affine open subset of `X`. -/
def sourceAffineLocally : AffineTargetMorphismProperty := fun X _ f _ =>
  âˆ€ U : X.affineOpens, P (f.appLE âŠ¤ U le_top)

/-- For `P` a property of ring homomorphisms, `affineLocally P` holds for `f : X âŸ¶ Y` if for each
affine open `U = Spec A âŠ† Y` and `V = Spec B âŠ† f â»Â¹' U`, the ring hom `A âŸ¶ B` satisfies `P`.
Also see `affineLocally_iff_affineOpens_le`. -/
abbrev affineLocally : MorphismProperty Scheme.{u} :=
  targetAffineLocally (sourceAffineLocally P)

theorem sourceAffineLocally_respectsIso (hâ‚ : RingHom.RespectsIso P) :
    (sourceAffineLocally P).toProperty.RespectsIso := by
  apply AffineTargetMorphismProperty.respectsIso_mk
  Â· introv H U
    have : IsIso (e.hom.appLE (e.hom ''áµ U) U.1 (e.hom.preimage_image_eq _).ge) :=
      inferInstanceAs (IsIso (e.hom.app _ â‰«
        X.presheaf.map (eqToHom (e.hom.preimage_image_eq _).symm).op))
    rw [â† Scheme.appLE_comp_appLE _ _ âŠ¤ (e.hom ''áµ U) U.1 le_top (e.hom.preimage_image_eq _).ge,
      hâ‚.cancel_right_isIso]
    exact H âŸ¨_, U.prop.image_of_isOpenImmersion e.homâŸ©
  Â· introv H U
    rw [Scheme.comp_appLE, hâ‚.cancel_left_isIso]
    exact H U

theorem affineLocally_respectsIso (h : RingHom.RespectsIso P) : (affineLocally P).RespectsIso :=
  letI := sourceAffineLocally_respectsIso P h
  inferInstance

open Scheme in
theorem sourceAffineLocally_morphismRestrict {X Y : Scheme.{u}} (f : X âŸ¶ Y)
    (U : Opens Y) (hU : IsAffineOpen U) :
    @sourceAffineLocally P _ _ (f âˆ£_ U) hU â†”
      âˆ€ (V : X.affineOpens) (e : V.1 â‰¤ f â»Â¹áµ U), P (f.appLE U V e) := by
  dsimp only [sourceAffineLocally]
  simp only [morphismRestrict_appLE]
  rw [(affineOpensRestrict (f â»Â¹áµ U)).forall_congr_left, Subtype.forall]
  refine forallâ‚‚_congr fun V h â†¦ ?_
  have := (affineOpensRestrict (f â»Â¹áµ U)).apply_symm_apply âŸ¨V, hâŸ©
  exact f.appLE_congr _ (Î¹Opens_image_top _) congr($(this).1.1) _

theorem affineLocally_iff_affineOpens_le {X Y : Scheme.{u}} (f : X âŸ¶ Y) :
    affineLocally.{u} P f â†”
      âˆ€ (U : Y.affineOpens) (V : X.affineOpens) (e : V.1 â‰¤ f â»Â¹áµ U.1), P (f.appLE U V e) :=
  forall_congr' fun U â†¦ sourceAffineLocally_morphismRestrict P f U U.2

theorem sourceAffineLocally_isLocal (hâ‚ : RingHom.RespectsIso P)
    (hâ‚‚ : RingHom.LocalizationPreserves P) (hâ‚ƒ : RingHom.OfLocalizationSpan P) :
    (sourceAffineLocally P).IsLocal := by
  constructor
  Â· exact sourceAffineLocally_respectsIso P hâ‚
  Â· intro X Y _ f r H
    rw [sourceAffineLocally_morphismRestrict]
    intro U hU
    have : X.basicOpen (f.appLE âŠ¤ U (by simp) r) = U := by
      simp only [Scheme.Hom.appLE, Opens.map_top, CommRingCat.coe_comp_of, RingHom.coe_comp,
        Function.comp_apply]
      rw [Scheme.basicOpen_res]
      simpa using hU
    rw [â† f.appLE_congr _ rfl this P,
      IsAffineOpen.appLE_eq_away_map f (isAffineOpen_top Y) U.2 _ r]
    apply (config := { allowSynthFailures := true }) hâ‚‚.away
    exact H U
  Â· introv hs hs' U
    apply hâ‚ƒ _ _ hs
    intro r
    simp_rw [sourceAffineLocally_morphismRestrict] at hs'
    have := hs' r âŸ¨X.basicOpen (f.appLE âŠ¤ U le_top r.1), U.2.basicOpen (f.appLE âŠ¤ U le_top r.1)âŸ©
      (by simpa [Scheme.Hom.appLE] using Scheme.basicOpen_restrict _ _ _)
    rwa [IsAffineOpen.appLE_eq_away_map f (isAffineOpen_top Y) U.2,
      â† hâ‚.is_localization_away_iff] at this

end affineLocally

/--
`HasRingHomProperty P Q` is a type class asserting that `P` is local at the target and the source,
and for `f : Spec B âŸ¶ Spec A`, it is equivalent to the ring hom property `Q`.
To make the proofs easier, we state it instead as
1. `Q` is local (See `RingHom.PropertyIsLocal`)
2. `P f` if and only if `Q` holds for every `Î“(Y, U) âŸ¶ Î“(X, V)` for all affine `U`, `V`.
-/
class HasRingHomProperty (P : MorphismProperty Scheme.{u})
    (Q : outParam (âˆ€ {R S : Type u} [CommRing R] [CommRing S], (R â†’+* S) â†’ Prop)) : Prop where
  isLocal_ringHomProperty : RingHom.PropertyIsLocal Q
  eq_affineLocally' : P = affineLocally Q

namespace HasRingHomProperty

variable (P : MorphismProperty Scheme.{u}) {Q} [HasRingHomProperty P Q]
variable {X Y Z : Scheme.{u}} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)

lemma eq_affineLocally : P = affineLocally Q := eq_affineLocally'

instance : HasAffineProperty P (sourceAffineLocally Q) where
  isLocal_affineProperty := sourceAffineLocally_isLocal _
    (HasRingHomProperty.isLocal_ringHomProperty P).respectsIso
    (HasRingHomProperty.isLocal_ringHomProperty P).LocalizationPreserves
    (HasRingHomProperty.isLocal_ringHomProperty P).ofLocalizationSpan
  eq_targetAffineLocally' := eq_affineLocally P

theorem appLE (H : P f) (U : Y.affineOpens) (V : X.affineOpens) (e) : Q (f.appLE U V e) := by
  rw [eq_affineLocally P, affineLocally_iff_affineOpens_le] at H
  exact H _ _ _

theorem app_top (H : P f) [IsAffine X] [IsAffine Y] : Q (f.app âŠ¤) := by
  rw [Scheme.Hom.app_eq_appLE]
  exact appLE P f H âŸ¨_, isAffineOpen_top _âŸ© âŸ¨_, isAffineOpen_top _âŸ© _

theorem comp_of_isOpenImmersion [IsOpenImmersion f] (H : P g) :
    P (f â‰« g) := by
  rw [eq_affineLocally P, affineLocally_iff_affineOpens_le] at H âŠ¢
  intro U V e
  have : IsIso (f.appLE (f ''áµ V) V.1 (f.preimage_image_eq _).ge) :=
    inferInstanceAs (IsIso (f.app _ â‰«
      X.presheaf.map (eqToHom (f.preimage_image_eq _).symm).op))
  rw [â† Scheme.appLE_comp_appLE _ _ _ (f ''áµ V) V.1
    (Set.image_subset_iff.mpr e) (f.preimage_image_eq _).ge,
    (isLocal_ringHomProperty P).respectsIso.cancel_right_isIso]
  exact H _ âŸ¨_, V.2.image_of_isOpenImmersion _âŸ© _

variable {P f}

lemma iff_appLE : P f â†” âˆ€ (U : Y.affineOpens) (V : X.affineOpens) (e), Q (f.appLE U V e) := by
  rw [eq_affineLocally P, affineLocally_iff_affineOpens_le]

theorem of_openCover [IsAffine Y]
    (ğ’° : X.OpenCover) [âˆ€ i, IsAffine (ğ’°.obj i)] (H : âˆ€ i, Q ((ğ’°.map i â‰« f).app âŠ¤)) :
    P f := by
  rw [HasAffineProperty.iff_of_isAffine (P := P)]
  intro U
  let S i : X.affineOpens := âŸ¨_, isAffineOpen_opensRange (ğ’°.map i)âŸ©
  induction U using of_affine_open_cover S ğ’°.iSup_opensRange with
  | basicOpen U r H =>
    simp_rw [Scheme.affineBasicOpen_coe,
      â† f.appLE_map (U := âŠ¤) le_top (homOfLE (X.basicOpen_le r)).op]
    apply (isLocal_ringHomProperty P).StableUnderComposition _ _ H
    have := U.2.isLocalization_basicOpen r
    apply (isLocal_ringHomProperty P).HoldsForLocalizationAway _ r
  | openCover U s hs H =>
    apply (isLocal_ringHomProperty P).OfLocalizationSpanTarget.ofIsLocalization
      (isLocal_ringHomProperty P).respectsIso _ _ hs
    rintro r
    refine âŸ¨_, _, _, IsAffineOpen.isLocalization_basicOpen U.2 r, ?_âŸ©
    rw [RingHom.algebraMap_toAlgebra, â† CommRingCat.comp_eq_ring_hom_comp, Scheme.Hom.appLE_map]
    exact H r
  | hU i =>
    specialize H i
    rw [â† (isLocal_ringHomProperty P).respectsIso.cancel_right_isIso _
      ((IsOpenImmersion.isoOfRangeEq (ğ’°.map i) (Scheme.Î¹Opens (S i).1)
      Subtype.range_coe.symm).inv.app _), â† Scheme.comp_app,
      IsOpenImmersion.isoOfRangeEq_inv_fac_assoc, Scheme.comp_app,
      Scheme.ofRestrict_app, Scheme.Hom.app_eq_appLE, Scheme.Hom.appLE_map] at H
    exact (f.appLE_congr _ rfl (by simp) Q).mp H

theorem iff_of_openCover [IsAffine Y] (ğ’° : X.OpenCover) [âˆ€ i, IsAffine (ğ’°.obj i)] :
    P f â†” âˆ€ i, Q ((ğ’°.map i â‰« f).app âŠ¤) :=
  âŸ¨fun H i â†¦ app_top P _ (comp_of_isOpenImmersion P (ğ’°.map i) f H), of_openCover ğ’°âŸ©

theorem iff_of_isAffine [IsAffine X] [IsAffine Y] :
    P f â†” Q (f.app âŠ¤) := by
  rw [iff_of_openCover (P := P) (Scheme.openCoverOfIsIso.{u} (ğŸ™ _))]
  simp

theorem Spec_iff {R S : CommRingCat.{u}} {Ï† : R âŸ¶ S} :
    P (Spec.map Ï†) â†” Q Ï† := by
  have H := (isLocal_ringHomProperty P).respectsIso
  rw [iff_of_isAffine (P := P), â† H.cancel_right_isIso _ (Scheme.Î“SpecIso _).hom,
    Scheme.Î“SpecIso_naturality, H.cancel_left_isIso]

theorem of_iSup_eq_top [IsAffine Y] {Î¹ : Type*}
    (U : Î¹ â†’ X.affineOpens) (hU : â¨† i, (U i : Opens X) = âŠ¤)
    (H : âˆ€ i, Q (f.appLE âŠ¤ (U i).1 le_top)) :
    P f := by
  have (i) : IsAffine ((X.openCoverOfSuprEqTop _ hU).obj i) := (U i).2
  refine of_openCover (X.openCoverOfSuprEqTop _ hU) fun i â†¦ ?_
  simpa [Scheme.Hom.app_eq_appLE] using (f.appLE_congr _ rfl (by simp) Q).mp (H i)

theorem iff_of_iSup_eq_top [IsAffine Y] {Î¹ : Type*}
    (U : Î¹ â†’ X.affineOpens) (hU : â¨† i, (U i : Opens X) = âŠ¤) :
    P f â†” âˆ€ i, Q (f.appLE âŠ¤ (U i).1 le_top) :=
  âŸ¨fun H _ â†¦ appLE P f H âŸ¨_, isAffineOpen_top _âŸ© _ le_top, of_iSup_eq_top U hUâŸ©

lemma HasAffineProperty.isLocalAtSource (P) {Q} [HasAffineProperty P Q]
    (H : âˆ€ {X Y : Scheme.{u}} (f : X âŸ¶ Y) [IsAffine Y] (ğ’° : Scheme.OpenCover.{u} X),
        Q f â†” âˆ€ i, Q (ğ’°.map i â‰« f)) : IsLocalAtSource P where
  iff_of_openCover' {X Y} f ğ’° := by
    simp_rw [IsLocalAtTarget.iff_of_iSup_eq_top _ (iSup_affineOpens_eq_top Y)]
    rw [forall_comm]
    refine forall_congr' fun U â†¦ ?_
    simp_rw [HasAffineProperty.iff_of_isAffine, morphismRestrict_comp]
    exact @H _ _ (f âˆ£_ U.1) U.2 (ğ’°.restrict (f â»Â¹áµ U.1))

instance : IsLocalAtSource P := by
  apply HasAffineProperty.isLocalAtSource
  intros X Y f _ ğ’°
  simp_rw [â† HasAffineProperty.iff_of_isAffine (P := P),
    iff_of_openCover ğ’°.affineRefinement.openCover,
    fun i â†¦ iff_of_openCover (P := P) (f := ğ’°.map i â‰« f) (ğ’°.obj i).affineCover]
  simp [Scheme.OpenCover.affineRefinement]

instance : P.ContainsIdentities where
  id_mem X := by
    rw [IsLocalAtTarget.iff_of_iSup_eq_top (P := P) _ (iSup_affineOpens_eq_top _)]
    intro U
    rw [morphismRestrict_id, iff_of_isAffine (P := P), Scheme.id_app]
    have := IsLocalization.at_units (.powers (1 : Î“(X âˆ£_áµ¤ ğŸ™ X â»Â¹áµ U, âŠ¤))) (by simp)
    exact (isLocal_ringHomProperty P).HoldsForLocalizationAway Î“(X âˆ£_áµ¤ ğŸ™ X â»Â¹áµ U, âŠ¤) 1

instance : P.IsStableUnderComposition where
  comp_mem {X Y Z} f g hf hg := by
    wlog hZ : IsAffine Z generalizing X Y Z
    Â· rw [IsLocalAtTarget.iff_of_iSup_eq_top (P := P) _ (iSup_affineOpens_eq_top _)]
      intro U
      rw [morphismRestrict_comp]
      exact this _ _ (IsLocalAtTarget.restrict hf _) (IsLocalAtTarget.restrict hg _) U.2
    wlog hY : IsAffine Y generalizing X Y
    Â· rw [IsLocalAtSource.iff_of_openCover (P := P) (Y.affineCover.pullbackCover f)]
      intro i
      rw [â† Scheme.OpenCover.pullbackHom_map_assoc]
      exact this _ _ (IsLocalAtTarget.of_isPullback (.of_hasPullback _ _) hf)
        (comp_of_isOpenImmersion _ _ _ hg) inferInstance
    wlog hX : IsAffine X generalizing X
    Â· rw [IsLocalAtSource.iff_of_openCover (P := P) X.affineCover]
      intro i
      rw [â† Category.assoc]
      exact this _ (comp_of_isOpenImmersion _ _ _ hf) inferInstance
    rw [iff_of_isAffine (P := P)] at hf hg âŠ¢
    exact (isLocal_ringHomProperty P).StableUnderComposition _ _ hg hf

instance : P.IsMultiplicative where

lemma of_isOpenImmersion [IsOpenImmersion f] : P f := IsLocalAtSource.of_isOpenImmersion f

lemma stableUnderBaseChange (hP : RingHom.StableUnderBaseChange Q) : P.StableUnderBaseChange := by
  apply HasAffineProperty.stableUnderBaseChange
  letI := HasAffineProperty.isLocal_affineProperty P
  apply AffineTargetMorphismProperty.StableUnderBaseChange.mk
  intros X Y S _ _ f g H
  rw [â† HasAffineProperty.iff_of_isAffine (P := P)] at H âŠ¢
  wlog hX : IsAffine Y generalizing Y
  Â· rw [IsLocalAtSource.iff_of_openCover (P := P)
      (Scheme.Pullback.openCoverOfRight Y.affineCover f g)]
    intro i
    simp only [Scheme.Pullback.openCoverOfRight_obj, Scheme.Pullback.openCoverOfRight_map,
      limit.lift_Ï€, PullbackCone.mk_pt, PullbackCone.mk_Ï€_app, Category.comp_id]
    apply this _ (comp_of_isOpenImmersion _ _ _ H) inferInstance
  rw [iff_of_isAffine (P := P)] at H âŠ¢
  exact hP.pullback_fst_app_top _ (isLocal_ringHomProperty P).respectsIso _ _ H

end HasRingHomProperty

end AlgebraicGeometry
