/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Topology.LocalAtTarget
import Mathlib.AlgebraicGeometry.Morphisms.Constructors

/-!

## Properties on the underlying functions of morphisms of schemes.

This file includes various results on properties of morphisms of schemes that come from properties
of the underlying map of topological spaces, including

- `Injective`
- `Surjective`
- `IsOpenMap`
- `IsClosedMap`
- `GeneralizingMap`
- `IsEmbedding`
- `IsOpenEmbedding`
- `IsClosedEmbedding`
- `DenseRange` (`IsDominant`)

-/

open CategoryTheory Topology TopologicalSpace

namespace AlgebraicGeometry

universe u v

section Injective

variable {X Y Z : Scheme.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)

instance : MorphismProperty.RespectsIso (topologically Function.Injective) :=
  topologically_respectsIso _ (fun e ‚Ü¶ e.injective) (fun _ _ hf hg ‚Ü¶ hg.comp hf)

instance injective_isZariskiLocalAtTarget :
    IsZariskiLocalAtTarget (topologically Function.Injective) := by
  refine topologically_isZariskiLocalAtTarget _ (fun _ s _ _ h ‚Ü¶ h.restrictPreimage s)
    fun f Œπ U H _ hf x‚ÇÅ x‚ÇÇ e ‚Ü¶ ?_
  obtain ‚ü®i, hxi‚ü© : ‚àÉ i, f x‚ÇÅ ‚àà U i := by simpa using congr(f x‚ÇÅ ‚àà $H)
  exact congr(($(@hf i ‚ü®x‚ÇÅ, hxi‚ü© ‚ü®x‚ÇÇ, show f x‚ÇÇ ‚àà U i from e ‚ñ∏ hxi‚ü© (Subtype.ext e))).1)

end Injective

section Surjective

variable {X Y Z : Scheme.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)

/-- A morphism of schemes is surjective if the underlying map is. -/
@[mk_iff]
class Surjective : Prop where
  surj : Function.Surjective f.base

lemma surjective_eq_topologically :
    @Surjective = topologically Function.Surjective := by ext; exact surjective_iff _

lemma Scheme.Hom.surjective (f : X.Hom Y) [Surjective f] : Function.Surjective f.base :=
  Surjective.surj

instance (priority := 100) [IsIso f] : Surjective f := ‚ü®f.homeomorph.surjective‚ü©

instance [Surjective f] [Surjective g] : Surjective (f ‚â´ g) := ‚ü®g.surjective.comp f.surjective‚ü©

lemma Surjective.of_comp [Surjective (f ‚â´ g)] : Surjective g where
  surj := Function.Surjective.of_comp (g := f.base) (f ‚â´ g).surjective

lemma Surjective.comp_iff [Surjective f] : Surjective (f ‚â´ g) ‚Üî Surjective g :=
  ‚ü®fun _ ‚Ü¶ of_comp f g, fun _ ‚Ü¶ inferInstance‚ü©

instance : MorphismProperty.RespectsIso @Surjective :=
  surjective_eq_topologically ‚ñ∏ topologically_respectsIso _ (fun e ‚Ü¶ e.surjective)
    (fun _ _ hf hg ‚Ü¶ hg.comp hf)

instance surjective_isZariskiLocalAtTarget : IsZariskiLocalAtTarget @Surjective := by
  have : MorphismProperty.RespectsIso @Surjective := inferInstance
  rw [surjective_eq_topologically] at this ‚ä¢
  refine topologically_isZariskiLocalAtTarget _ (fun _ s _ _ h ‚Ü¶ h.restrictPreimage s) ?_
  intro Œ± Œ≤ _ _ f Œπ U H _ hf x
  obtain ‚ü®i, hxi‚ü© : ‚àÉ i, x ‚àà U i := by simpa using congr(x ‚àà $H)
  obtain ‚ü®‚ü®y, _‚ü©, hy‚ü© := hf i ‚ü®x, hxi‚ü©
  exact ‚ü®y, congr(($hy).1)‚ü©

@[simp]
lemma range_eq_univ [Surjective f] : Set.range f.base = Set.univ := by
  simpa [Set.range_eq_univ] using f.surjective

lemma range_eq_range_of_surjective {S : Scheme.{u}} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) (e : X ‚ü∂ Y)
    [Surjective e] (hge : e ‚â´ g = f) : Set.range f.base = Set.range g.base := by
  rw [‚Üê hge]
  simp [Set.range_comp]

lemma mem_range_iff_of_surjective {S : Scheme.{u}} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) (e : X ‚ü∂ Y)
    [Surjective e] (hge : e ‚â´ g = f) (s : S) : s ‚àà Set.range f.base ‚Üî s ‚àà Set.range g.base := by
  rw [range_eq_range_of_surjective f g e hge]

lemma Surjective.sigmaDesc_of_union_range_eq_univ {X : Scheme.{u}}
    {Œπ : Type v} [Small.{u} Œπ] {Y : Œπ ‚Üí Scheme.{u}} {f : ‚àÄ i, Y i ‚ü∂ X}
    (H : ‚ãÉ i, Set.range (f i).base = Set.univ) : Surjective (Limits.Sigma.desc f) := by
  refine ‚ü®fun x ‚Ü¶ ?_‚ü©
  simp_rw [Set.eq_univ_iff_forall, Set.mem_iUnion] at H
  obtain ‚ü®i, x, rfl‚ü© := H x
  use (Limits.Sigma.Œπ (fun i ‚Ü¶ Y i) i).base x
  rw [‚Üê Scheme.Hom.comp_apply, Limits.Sigma.Œπ_desc]

instance {X : Scheme.{u}} {P : MorphismProperty Scheme.{u}} (ùí∞ : X.Cover (Scheme.precoverage P)) :
    Surjective (Limits.Sigma.desc fun i ‚Ü¶ ùí∞.f i) :=
  Surjective.sigmaDesc_of_union_range_eq_univ ùí∞.iUnion_range

end Surjective

section Injective

instance injective_isStableUnderComposition :
    MorphismProperty.IsStableUnderComposition (topologically (Function.Injective ¬∑)) where
  comp_mem _ _ hf hg := hg.comp hf

end Injective

section IsOpenMap

instance : (topologically IsOpenMap).RespectsIso :=
  topologically_respectsIso _ (fun e ‚Ü¶ e.isOpenMap) (fun _ _ hf hg ‚Ü¶ hg.comp hf)

instance isOpenMap_isZariskiLocalAtTarget : IsZariskiLocalAtTarget (topologically IsOpenMap) :=
  topologically_isZariskiLocalAtTarget' _ fun _ _ _ hU _ ‚Ü¶ hU.isOpenMap_iff_restrictPreimage

instance : IsZariskiLocalAtSource (topologically IsOpenMap) :=
  topologically_isZariskiLocalAtSource' (fun _ ‚Ü¶ _) fun _ _ _ hU _ ‚Ü¶ hU.isOpenMap_iff_comp

end IsOpenMap

section IsClosedMap

instance : (topologically IsClosedMap).RespectsIso :=
  topologically_respectsIso _ (fun e ‚Ü¶ e.isClosedMap) (fun _ _ hf hg ‚Ü¶ hg.comp hf)

instance isClosedMap_isZariskiLocalAtTarget : IsZariskiLocalAtTarget (topologically IsClosedMap) :=
  topologically_isZariskiLocalAtTarget' _ fun _ _ _ hU _ ‚Ü¶ hU.isClosedMap_iff_restrictPreimage

end IsClosedMap

section IsEmbedding

instance : (topologically IsEmbedding).RespectsIso :=
  topologically_respectsIso _ (fun e ‚Ü¶ e.isEmbedding) (fun _ _ hf hg ‚Ü¶ hg.comp hf)

instance isEmbedding_isZariskiLocalAtTarget : IsZariskiLocalAtTarget (topologically IsEmbedding) :=
  topologically_isZariskiLocalAtTarget' _ fun _ _ _ hU ‚Ü¶ hU.isEmbedding_iff_restrictPreimage

end IsEmbedding

section IsOpenEmbedding

instance : (topologically IsOpenEmbedding).RespectsIso :=
  topologically_respectsIso _ (fun e ‚Ü¶ e.isOpenEmbedding) (fun _ _ hf hg ‚Ü¶ hg.comp hf)

instance isOpenEmbedding_isZariskiLocalAtTarget :
    IsZariskiLocalAtTarget (topologically IsOpenEmbedding) :=
  topologically_isZariskiLocalAtTarget' _ fun _ _ _ hU ‚Ü¶ hU.isOpenEmbedding_iff_restrictPreimage

end IsOpenEmbedding

section IsClosedEmbedding

instance : (topologically IsClosedEmbedding).RespectsIso :=
  topologically_respectsIso _ (fun e ‚Ü¶ e.isClosedEmbedding) (fun _ _ hf hg ‚Ü¶ hg.comp hf)

instance isClosedEmbedding_isZariskiLocalAtTarget :
    IsZariskiLocalAtTarget (topologically IsClosedEmbedding) :=
  topologically_isZariskiLocalAtTarget' _ fun _ _ _ hU ‚Ü¶ hU.isClosedEmbedding_iff_restrictPreimage

end IsClosedEmbedding

section IsDominant

variable {X Y Z : Scheme.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)

/-- A morphism of schemes is dominant if the underlying map has dense range. -/
@[mk_iff]
class IsDominant : Prop where
  denseRange : DenseRange f.base

lemma dominant_eq_topologically :
    @IsDominant = topologically DenseRange := by ext; exact isDominant_iff _

lemma Scheme.Hom.denseRange (f : X.Hom Y) [IsDominant f] : DenseRange f.base :=
  IsDominant.denseRange

instance (priority := 100) [Surjective f] : IsDominant f := ‚ü®f.surjective.denseRange‚ü©

instance [IsDominant f] [IsDominant g] : IsDominant (f ‚â´ g) :=
  ‚ü®g.denseRange.comp f.denseRange g.base.hom.2‚ü©

instance : MorphismProperty.IsMultiplicative @IsDominant where
  id_mem := fun _ ‚Ü¶ inferInstance
  comp_mem := fun _ _ _ _ ‚Ü¶ inferInstance

lemma IsDominant.of_comp [H : IsDominant (f ‚â´ g)] : IsDominant g := by
  rw [isDominant_iff, denseRange_iff_closure_range, ‚Üê Set.univ_subset_iff] at H ‚ä¢
  exact H.trans (closure_mono (Set.range_comp_subset_range f.base g.base))

lemma IsDominant.comp_iff [IsDominant f] : IsDominant (f ‚â´ g) ‚Üî IsDominant g :=
  ‚ü®fun _ ‚Ü¶ of_comp f g, fun _ ‚Ü¶ inferInstance‚ü©

instance IsDominant.respectsIso : MorphismProperty.RespectsIso @IsDominant :=
  MorphismProperty.respectsIso_of_isStableUnderComposition fun _ _ f (_ : IsIso f) ‚Ü¶ inferInstance

instance IsDominant.isZariskiLocalAtTarget : IsZariskiLocalAtTarget @IsDominant :=
  have : MorphismProperty.RespectsIso (topologically DenseRange) :=
    dominant_eq_topologically ‚ñ∏ IsDominant.respectsIso
  dominant_eq_topologically ‚ñ∏ topologically_isZariskiLocalAtTarget' DenseRange
    fun _ _ _ hU _ ‚Ü¶ hU.denseRange_iff_restrictPreimage

lemma surjective_of_isDominant_of_isClosed_range (f : X ‚ü∂ Y) [IsDominant f]
    (hf : IsClosed (Set.range f.base)) :
    Surjective f :=
  ‚ü®by rw [‚Üê Set.range_eq_univ, ‚Üê hf.closure_eq, f.denseRange.closure_range]‚ü©

lemma IsDominant.of_comp_of_isOpenImmersion
    (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [H : IsDominant (f ‚â´ g)] [IsOpenImmersion g] :
    IsDominant f := by
  rw [isDominant_iff, DenseRange] at H ‚ä¢
  simp only [Scheme.Hom.comp_base, TopCat.coe_comp, Set.range_comp] at H
  convert H.preimage g.isOpenEmbedding.isOpenMap using 1
  rw [Set.preimage_image_eq _ g.isOpenEmbedding.injective]

end IsDominant

section SpecializingMap

open TopologicalSpace

instance specializingMap_respectsIso : (topologically @SpecializingMap).RespectsIso := by
  apply topologically_respectsIso
  ¬∑ introv
    exact f.isClosedMap.specializingMap
  ¬∑ introv hf hg
    exact hf.comp hg

instance specializingMap_isZariskiLocalAtTarget :
    IsZariskiLocalAtTarget (topologically @SpecializingMap) := by
  apply topologically_isZariskiLocalAtTarget
  ¬∑ introv _ _ hf
    rw [specializingMap_iff_closure_singleton_subset] at hf ‚ä¢
    intro ‚ü®x, hx‚ü© ‚ü®y, hy‚ü© hcl
    simp only [closure_subtype, Set.restrictPreimage_mk, Set.image_singleton] at hcl
    obtain ‚ü®a, ha, hay‚ü© := hf x hcl
    rw [‚Üê specializes_iff_mem_closure] at hcl
    exact ‚ü®‚ü®a, by simp [hay, hy]‚ü©, by simpa [closure_subtype], by simpa‚ü©
  ¬∑ introv hU _ hsp
    simp_rw [specializingMap_iff_closure_singleton_subset] at hsp ‚ä¢
    intro x y hy
    have : ‚àÉ i, y ‚àà U i := Opens.mem_iSup.mp (hU ‚ñ∏ Opens.mem_top _)
    obtain ‚ü®i, hi‚ü© := this
    rw [‚Üê specializes_iff_mem_closure] at hy
    have hfx : f x ‚àà U i := (U i).2.stableUnderGeneralization hy hi
    have hy : (‚ü®y, hi‚ü© : U i) ‚àà closure {‚ü®f x, hfx‚ü©} := by
      simp only [closure_subtype, Set.image_singleton]
      rwa [‚Üê specializes_iff_mem_closure]
    obtain ‚ü®a, ha, hay‚ü© := hsp i ‚ü®x, hfx‚ü© hy
    rw [closure_subtype] at ha
    simp only [Opens.carrier_eq_coe, Set.image_singleton] at ha
    apply_fun Subtype.val at hay
    simp only [Opens.carrier_eq_coe, Set.restrictPreimage_coe] at hay
    use a.val, ha, hay

end SpecializingMap

section GeneralizingMap

instance : (topologically GeneralizingMap).RespectsIso :=
  topologically_respectsIso _ (fun f ‚Ü¶ f.isOpenEmbedding.generalizingMap
    f.isOpenEmbedding.isOpen_range.stableUnderGeneralization) (fun _ _ hf hg ‚Ü¶ hf.comp hg)

instance : IsZariskiLocalAtSource (topologically GeneralizingMap) :=
  topologically_isZariskiLocalAtSource' (fun _ ‚Ü¶ _) fun _ _ _ hU _ ‚Ü¶ hU.generalizingMap_iff_comp

instance : IsZariskiLocalAtTarget (topologically GeneralizingMap) :=
  topologically_isZariskiLocalAtTarget' (fun _ ‚Ü¶ _) fun _ _ _ hU _ ‚Ü¶
    hU.generalizingMap_iff_restrictPreimage

end GeneralizingMap

end AlgebraicGeometry
