/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.Preimmersion
import Mathlib.AlgebraicGeometry.Morphisms.QuasiSeparated
import Mathlib.AlgebraicGeometry.IdealSheaf.Basic
import Mathlib.CategoryTheory.Adjunction.Opposites

/-!
# Subscheme associated to an ideal sheaf

We construct the subscheme associated to an ideal sheaf.

## Main definition
* `AlgebraicGeometry.Scheme.IdealSheafData.subscheme`: The subscheme associated to an ideal sheaf.
* `AlgebraicGeometry.Scheme.IdealSheafData.subschemeÎ¹`: The inclusion from the subscheme.
* `AlgebraicGeometry.Scheme.Hom.image`: The scheme-theoretic image of a morphism.
* `AlgebraicGeometry.Scheme.kerAdjunction`:
  The adjunction between taking kernels and taking the associated subscheme.

## Note

Some instances are in `Mathlib/AlgebraicGeometry/Morphisms/ClosedImmersion` and
`Mathlib/AlgebraicGeometry/Morphisms/Separated` because they need more API to prove.

-/

open CategoryTheory TopologicalSpace PrimeSpectrum Limits

universe u

namespace AlgebraicGeometry.Scheme.IdealSheafData

variable {X : Scheme.{u}}

variable (I : IdealSheafData X)

/-- `Spec (ğ’ªâ‚“(U)/I(U))`, the object to be glued into the closed subscheme. -/
def glueDataObj (U : X.affineOpens) : Scheme :=
  Spec <| .of <| Î“(X, U) â§¸ I.ideal U

/-- `Spec (ğ’ªâ‚“(U)/I(U)) âŸ¶ Spec (ğ’ªâ‚“(U)) = U`, the closed immersion into `U`. -/
noncomputable
def glueDataObjÎ¹ (U : X.affineOpens) : I.glueDataObj U âŸ¶ U.1 :=
  Spec.map (CommRingCat.ofHom (Ideal.Quotient.mk _)) â‰« U.2.isoSpec.inv

instance (U : X.affineOpens) : IsPreimmersion (I.glueDataObjÎ¹ U) :=
  have : IsPreimmersion (Spec.map (CommRingCat.ofHom (Ideal.Quotient.mk (I.ideal U)))) :=
    .mk_SpecMap
      (isClosedEmbedding_comap_of_surjective _ _ Ideal.Quotient.mk_surjective).isEmbedding
      (RingHom.surjectiveOnStalks_of_surjective Ideal.Quotient.mk_surjective)
  .comp _ _

lemma glueDataObjÎ¹_Î¹ (U : X.affineOpens) : I.glueDataObjÎ¹ U â‰« U.1.Î¹ =
    Spec.map (CommRingCat.ofHom (Ideal.Quotient.mk _)) â‰« U.2.fromSpec := by
  rw [glueDataObjÎ¹, Category.assoc]; rfl

lemma ker_glueDataObjÎ¹_appTop (U : X.affineOpens) :
    RingHom.ker (I.glueDataObjÎ¹ U).appTop.hom = (I.ideal U).comap U.1.topIso.hom.hom := by
  let Ï† := CommRingCat.ofHom (Ideal.Quotient.mk (I.ideal U))
  rw [â† Ideal.mk_ker (I := I.ideal _)]
  change RingHom.ker (Spec.map Ï† â‰« _).appTop.hom = (RingHom.ker Ï†.hom).comap _
  rw [â† RingHom.ker_equiv_comp _ (Scheme.Î“SpecIso _).commRingCatIsoToRingEquiv, RingHom.comap_ker,
    RingEquiv.toRingHom_eq_coe, Iso.commRingCatIsoToRingEquiv_toRingHom, â† CommRingCat.hom_comp,
    â† CommRingCat.hom_comp]
  congr 2
  simp only [Scheme.Hom.comp_app, TopologicalSpace.Opens.map_top, Category.assoc,
    Scheme.Î“SpecIso_naturality, Scheme.Opens.topIso_hom]
  rw [â† Scheme.Hom.appTop, U.2.isoSpec_inv_appTop, Category.assoc, Iso.inv_hom_id_assoc]
  simp only [Scheme.Opens.topIso_hom]

open scoped Set.Notation in
lemma range_glueDataObjÎ¹ (U : X.affineOpens) :
    Set.range (I.glueDataObjÎ¹ U) =
      U.2.isoSpec.inv '' PrimeSpectrum.zeroLocus (I.ideal U) := by
  simp only [glueDataObjÎ¹, Scheme.Hom.comp_base, TopCat.coe_comp, Set.range_comp]
  erw [range_comap_of_surjective]
  swap; Â· exact Ideal.Quotient.mk_surjective
  simp only [Ideal.mk_ker, CommRingCat.hom_ofHom]

lemma range_glueDataObjÎ¹_Î¹ (U : X.affineOpens) :
    Set.range (I.glueDataObjÎ¹ U â‰« U.1.Î¹) = X.zeroLocus (U := U) (I.ideal U) âˆ© U := by
  simp only [Scheme.Hom.comp_base, TopCat.coe_comp, Set.range_comp, range_glueDataObjÎ¹]
  rw [â† Set.image_comp, â† TopCat.coe_comp, â† Scheme.Hom.comp_base, IsAffineOpen.isoSpec_inv_Î¹,
    IsAffineOpen.fromSpec_image_zeroLocus]

/-- The underlying space of `Spec (ğ’ªâ‚“(U)/I(U))` is homeomorphic to its image in `X`. -/
noncomputable
def glueDataObjCarrierIso (U : X.affineOpens) :
    (I.glueDataObj U).carrier â‰… TopCat.of â†‘(X.zeroLocus (U := U) (I.ideal U) âˆ© U) :=
  TopCat.isoOfHomeo ((I.glueDataObjÎ¹ U â‰« U.1.Î¹).isEmbedding.toHomeomorph.trans
    (.setCongr (I.range_glueDataObjÎ¹_Î¹ U)))

/-- The open immersion `Spec Î“(ğ’ªâ‚“/I, U) âŸ¶ Spec Î“(ğ’ªâ‚“/I, V)` if `U â‰¤ V`. -/
noncomputable
def glueDataObjMap {U V : X.affineOpens} (h : U â‰¤ V) : I.glueDataObj U âŸ¶ I.glueDataObj V :=
  Spec.map (CommRingCat.ofHom (Ideal.quotientMap _ _ (I.ideal_le_comap_ideal h)))

lemma isLocalization_away {U V : X.affineOpens}
    (h : U â‰¤ V) (f : Î“(X, V.1)) (hU : U = X.affineBasicOpen f) :
      letI := (Ideal.quotientMap _ _ (I.ideal_le_comap_ideal h)).toAlgebra
      IsLocalization.Away (Ideal.Quotient.mk (I.ideal V) f) (Î“(X, U) â§¸ (I.ideal U)) := by
  letI := (Ideal.quotientMap _ _ (I.ideal_le_comap_ideal h)).toAlgebra
  let f' : Î“(X, V) â§¸ I.ideal V := Ideal.Quotient.mk _ f
  letI := (X.presheaf.map (homOfLE (X := X.Opens) h).op).hom.toAlgebra
  have : IsLocalization.Away f Î“(X, U) := by
    subst hU; exact V.2.isLocalization_of_eq_basicOpen _ _ rfl
  simp only [IsLocalization.Away, â† Submonoid.map_powers]
  refine IsLocalization.of_surjective _ _ _ Ideal.Quotient.mk_surjective _
    Ideal.Quotient.mk_surjective ?_ ?_
  Â· simp [RingHom.algebraMap_toAlgebra, Ideal.quotientMap_comp_mk]; rfl
  Â· simp only [Ideal.mk_ker, RingHom.algebraMap_toAlgebra, I.map_ideal', le_refl]

instance isOpenImmersion_glueDataObjMap {V : X.affineOpens} (f : Î“(X, V.1)) :
    IsOpenImmersion (I.glueDataObjMap (X.affineBasicOpen_le f)) := by
  letI := (Ideal.quotientMap _ _ (I.ideal_le_comap_ideal (X.affineBasicOpen_le f))).toAlgebra
  have := I.isLocalization_away (X.affineBasicOpen_le f) f rfl
  exact IsOpenImmersion.of_isLocalization (Ideal.Quotient.mk _ f)

lemma opensRange_glueDataObjMap {V : X.affineOpens} (f : Î“(X, V.1)) :
      (I.glueDataObjMap (X.affineBasicOpen_le f)).opensRange =
        (I.glueDataObjÎ¹ V) â»Â¹áµ (V.1.Î¹ â»Â¹áµ X.basicOpen f) := by
  letI := (Ideal.quotientMap _ _ (I.ideal_le_comap_ideal (X.affineBasicOpen_le f))).toAlgebra
  let f' : Î“(X, V) â§¸ I.ideal V := Ideal.Quotient.mk _ f
  have := I.isLocalization_away (X.affineBasicOpen_le f) f rfl
  ext1
  refine (localization_away_comap_range _ f').trans ?_
  rw [â† comap_basicOpen, â† V.2.fromSpec_preimage_basicOpen,
    â† Scheme.Hom.comp_preimage, glueDataObjÎ¹_Î¹]
  rfl

@[reassoc (attr := simp)]
lemma glueDataObjMap_glueDataObjÎ¹ {U V : X.affineOpens} (h : U â‰¤ V) :
    I.glueDataObjMap h â‰« I.glueDataObjÎ¹ V = I.glueDataObjÎ¹ U â‰« X.homOfLE h := by
  rw [glueDataObjMap, glueDataObjÎ¹, â† Spec.map_comp_assoc, â† CommRingCat.ofHom_comp,
    Ideal.quotientMap_comp_mk, CommRingCat.ofHom_comp, Spec.map_comp_assoc, glueDataObjÎ¹,
    Category.assoc]
  congr 1
  rw [Iso.eq_inv_comp, IsAffineOpen.isoSpec_hom, CommRingCat.ofHom_hom]
  erw [Scheme.Opens.toSpecÎ“_SpecMap_presheaf_map_assoc U.1 V.1 h]
  rw [â† IsAffineOpen.isoSpec_hom V.2, Iso.hom_inv_id, Category.comp_id]

lemma ideal_le_ker_glueDataObjÎ¹ (U V : X.affineOpens) :
    I.ideal V â‰¤ RingHom.ker (U.1.Î¹.app V.1 â‰« (I.glueDataObjÎ¹ U).app _).hom := by
  intro x hx
  apply (I.glueDataObj U).IsSheaf.section_ext
  intro p hp
  obtain âŸ¨f, g, hfg, hfâŸ© := exists_basicOpen_le_affine_inter U.2 V.2 (I.glueDataObjÎ¹ U p).1
      âŸ¨(I.glueDataObjÎ¹ U p).2, hpâŸ©
  refine âŸ¨(I.glueDataObjÎ¹ U â»Â¹áµ U.1.Î¹ â»Â¹áµ X.basicOpen f),
    fun x hx â†¦ X.basicOpen_le g (hfg â–¸ hx), hf, ?_âŸ©
  have := Hom.isIso_app (I.glueDataObjMap (X.affineBasicOpen_le f))
    (I.glueDataObjÎ¹ U â»Â¹áµ U.1.Î¹ â»Â¹áµ X.basicOpen f) (by rw [opensRange_glueDataObjMap])
  apply ((ConcreteCategory.isIso_iff_bijective _).mp this).1
  simp only [map_zero, â† RingHom.comp_apply,
    â† CommRingCat.hom_comp, Category.assoc]
  simp only [Scheme.Hom.app_eq_appLE, homOfLE_leOfHom, Scheme.Hom.map_appLE,
    Scheme.Hom.appLE_comp_appLE, Category.assoc, glueDataObjMap_glueDataObjÎ¹_assoc]
  rw [Scheme.Hom.appLE]
  have H : (X.homOfLE (X.basicOpen_le f) â‰« U.1.Î¹) â»Â¹áµ V.1 = âŠ¤ := by
    simp only [Scheme.homOfLE_Î¹, â† top_le_iff]
    exact fun x _ â†¦ (hfg.trans_le (X.basicOpen_le g)) x.2
  simp only [Scheme.Hom.comp_app, Scheme.Opens.Î¹_app, Scheme.homOfLE_app, â† Functor.map_comp_assoc,
    Scheme.Hom.app_eq _ H, Scheme.Opens.toScheme_presheaf_map, â† Functor.map_comp, Category.assoc]
  simp only [CommRingCat.hom_comp, RingHom.comp_apply]
  convert RingHom.map_zero _ using 2
  rw [â† RingHom.mem_ker, ker_glueDataObjÎ¹_appTop, â† Ideal.mem_comap, Ideal.comap_comap,
    â† CommRingCat.hom_comp]
  simp only [Scheme.affineBasicOpen_coe, homOfLE_leOfHom, Scheme.Hom.comp_base,
    TopologicalSpace.Opens.map_comp_obj, eqToHom_op, eqToHom_unop, â† Functor.map_comp,
    Scheme.Opens.topIso_hom, Category.assoc]
  exact I.ideal_le_comap_ideal (U := X.affineBasicOpen f) (V := V)
    (hfg.trans_le (X.basicOpen_le g)) hx

/-- (Implementation) The intersections `Spec Î“(ğ’ªâ‚“/I, U) âˆ© V` useful for gluing. -/
private noncomputable
abbrev glueDataObjPullback (U V : X.affineOpens) : Scheme :=
  pullback (I.glueDataObjÎ¹ U) (X.homOfLE (U := U.1 âŠ“ V.1) inf_le_left)

/-- (Implementation) Transition maps in the glue data for `ğ’ªâ‚“/I`. -/
private noncomputable
def glueDataT (U V : X.affineOpens) :
    I.glueDataObjPullback U V âŸ¶ I.glueDataObjPullback V U := by
  letI F := pullback.snd (I.glueDataObjÎ¹ U) (X.homOfLE (inf_le_left (b := V.1)))
  refine pullback.lift ((F â‰« X.homOfLE inf_le_right â‰«
    V.2.isoSpec.hom).liftQuotient _ ?_) (F â‰« X.homOfLE (by simp)) ?_
  Â· intro x hx
    simp only [Hom.comp_app, Hom.comp_base, TopologicalSpace.Opens.map_comp_obj,
      TopologicalSpace.Opens.map_top, homOfLE_app, homOfLE_leOfHom, Category.assoc, RingHom.mem_ker]
    convert_to (U.1.Î¹.app V.1 â‰« (F â‰« X.homOfLE inf_le_left).appLE (U.1.Î¹ â»Â¹áµ V.1) âŠ¤
      (by rw [â† Scheme.Hom.comp_preimage, Category.assoc, X.homOfLE_Î¹]
          exact fun x _ â†¦ by simpa using (F x).2.2)).hom x = 0 using 3
    Â· simp only [homOfLE_leOfHom, Opens.Î¹_app, Hom.comp_appLE, homOfLE_app]
      have H : âŠ¤ â‰¤ X.homOfLE (inf_le_left (b := V.1)) â»Â¹áµ U.1.Î¹ â»Â¹áµ V.1 := by
        rw [â† Scheme.Hom.comp_preimage, X.homOfLE_Î¹]; exact fun x _ â†¦ by simpa using x.2.2
      rw [â† F.map_appLE (show âŠ¤ â‰¤ F â»Â¹áµ âŠ¤ from le_rfl) (homOfLE H).op]
      simp only [homOfLE_leOfHom, Opens.toScheme_presheaf_map, Quiver.Hom.unop_op,
        Hom.opensFunctor_map_homOfLE, â† Functor.map_comp_assoc, IsAffineOpen.isoSpec_hom_appTop,
        Opens.topIso_inv, eqToHom_op, homOfLE_leOfHom, Category.assoc,
        Iso.inv_hom_id_assoc, F.app_eq_appLE]
      rfl
    Â· have : (U.1.Î¹.app V.1 â‰« (I.glueDataObjÎ¹ U).app (U.1.Î¹ â»Â¹áµ V.1)).hom x = 0 :=
        I.ideal_le_ker_glueDataObjÎ¹ U V hx
      simp_rw [F, â† pullback.condition]
      simp only [Scheme.Opens.Î¹_app, CommRingCat.hom_comp, RingHom.coe_comp,
        Function.comp_apply, Scheme.Hom.appLE, Scheme.Hom.comp_app, Category.assoc] at this âŠ¢
      simp only [this, map_zero]
  Â· conv_lhs => enter [2]; rw [glueDataObjÎ¹]
    rw [Scheme.Hom.liftQuotient_comp_assoc, Category.assoc, Category.assoc, Iso.hom_inv_id,
      Category.comp_id, Category.assoc, X.homOfLE_homOfLE]

@[reassoc (attr := simp)]
private lemma glueDataT_snd (U V : X.affineOpens) :
    I.glueDataT U V â‰« pullback.snd _ _ = pullback.snd _ _ â‰« X.homOfLE (by simp) :=
  pullback.lift_snd _ _ _

@[reassoc (attr := simp)]
private lemma glueDataT_fst (U V : X.affineOpens) :
    I.glueDataT U V â‰« pullback.fst _ _ â‰« glueDataObjÎ¹ _ _ =
      pullback.snd _ _ â‰« X.homOfLE inf_le_right := by
  refine (pullback.lift_fst_assoc _ _ _ _).trans ?_
  conv_lhs => enter [2]; rw [glueDataObjÎ¹]
  rw [Scheme.Hom.liftQuotient_comp_assoc, Category.assoc, Category.assoc, Iso.hom_inv_id,
    Category.comp_id]

/-- (Implementation) `t'` in the glue data for `ğ’ªâ‚“/I`. -/
private noncomputable
def glueDataT'Aux (U V W Uâ‚€ : X.affineOpens) (hUâ‚€ : U.1 âŠ“ W â‰¤ Uâ‚€) :
    pullback
      (pullback.fst _ _ : I.glueDataObjPullback U V âŸ¶ _)
      (pullback.fst _ _ : I.glueDataObjPullback U W âŸ¶ _) âŸ¶ I.glueDataObjPullback V Uâ‚€ :=
  pullback.lift
    (pullback.fst _ _ â‰« I.glueDataT U V â‰« pullback.fst _ _)
    (IsOpenImmersion.lift (V.1 âŠ“ Uâ‚€.1).Î¹
      (pullback.fst _ _ â‰« pullback.fst _ _ â‰« I.glueDataObjÎ¹ U â‰« U.1.Î¹) (by
      simp only [Scheme.Opens.range_Î¹, TopologicalSpace.Opens.coe_inf, Set.subset_inter_iff]
      constructor
      Â· rw [pullback.condition_assoc (f := I.glueDataObjÎ¹ U), X.homOfLE_Î¹,
          â† Category.assoc, Scheme.Hom.comp_base, TopCat.coe_comp]
        exact (Set.range_comp_subset_range _ _).trans (by simp)
      Â· rw [pullback.condition_assoc, pullback.condition_assoc, X.homOfLE_Î¹,
          â† Category.assoc, Scheme.Hom.comp_base, TopCat.coe_comp]
        exact (Set.range_comp_subset_range _ _).trans (by simpa using hUâ‚€))) (by
      rw [â† cancel_mono (Scheme.Opens.Î¹ _)]
      simp [pullback.condition_assoc])

@[reassoc (attr := simp)]
private lemma glueDataT'Aux_fst (U V W Uâ‚€ : X.affineOpens) (hUâ‚€ : U.1 âŠ“ W â‰¤ Uâ‚€) :
    I.glueDataT'Aux U V W Uâ‚€ hUâ‚€ â‰« pullback.fst _ _ =
      pullback.fst _ _ â‰« I.glueDataT U V â‰« pullback.fst _ _ := pullback.lift_fst _ _ _

@[reassoc (attr := simp)]
private lemma glueDataT'Aux_snd_Î¹ (U V W Uâ‚€ : X.affineOpens) (hUâ‚€ : U.1 âŠ“ W â‰¤ Uâ‚€) :
    I.glueDataT'Aux U V W Uâ‚€ hUâ‚€ â‰« pullback.snd _ _ â‰« (V.1 âŠ“ Uâ‚€.1).Î¹ =
      pullback.fst _ _ â‰« pullback.fst _ _ â‰« I.glueDataObjÎ¹ U â‰« U.1.Î¹ :=
  (pullback.lift_snd_assoc _ _ _ _).trans (IsOpenImmersion.lift_fac _ _ _)

/-- (Implementation) The glue data for `ğ’ªâ‚“/I`. -/
@[simps]
private noncomputable
def glueData : Scheme.GlueData where
  J := X.affineOpens
  U := I.glueDataObj
  V ij := I.glueDataObjPullback ij.1 ij.2
  f i j := pullback.fst _ _
  f_id i :=
    have : IsIso (X.homOfLE (inf_le_left (a := i.1) (b := i.1))) :=
      âŸ¨X.homOfLE (by simp), by simp, by simpâŸ©
    inferInstance
  t i j := I.glueDataT i j
  t_id i := by
    apply pullback.hom_ext
    Â· rw [â† cancel_mono (glueDataObjÎ¹ _ _)]
      simp [pullback.condition]
    Â· simp
  t' i j k := pullback.lift
    (I.glueDataT'Aux _ _ _ _ inf_le_right) (I.glueDataT'Aux _ _ _ _ inf_le_left) (by simp)
  t_fac i j k := by
    apply pullback.hom_ext
    Â· rw [â† cancel_mono (glueDataObjÎ¹ _ _)]
      simp
    Â· rw [â† cancel_mono (Scheme.Opens.Î¹ _)]
      simp [pullback.condition_assoc]
  cocycle i j k := by
    dsimp only
    apply pullback.hom_ext
    Â· apply pullback.hom_ext
      Â· rw [â† cancel_mono (glueDataObjÎ¹ _ _), â† cancel_mono (Scheme.Opens.Î¹ _)]
        simp only [Category.assoc, limit.lift_Ï€, PullbackCone.mk_pt, PullbackCone.mk_Ï€_app,
          glueDataT'Aux_fst, limit.lift_Ï€_assoc, cospan_left, glueDataT_fst, Scheme.homOfLE_Î¹,
          glueDataT'Aux_snd_Î¹, glueDataT'Aux_fst_assoc, glueDataT_fst_assoc, Category.id_comp]
        rw [pullback.condition_assoc (f := I.glueDataObjÎ¹ i)]
        simp
      Â· rw [â† cancel_mono (Scheme.Opens.Î¹ _)]
        simp [pullback.condition_assoc]
    Â· apply pullback.hom_ext
      Â· rw [â† cancel_mono (glueDataObjÎ¹ _ _), â† cancel_mono (Scheme.Opens.Î¹ _)]
        simp only [Category.assoc, limit.lift_Ï€, PullbackCone.mk_pt, PullbackCone.mk_Ï€_app,
          glueDataT'Aux_fst, limit.lift_Ï€_assoc, cospan_left, glueDataT_fst, Scheme.homOfLE_Î¹,
          glueDataT'Aux_snd_Î¹, glueDataT'Aux_fst_assoc, glueDataT_fst_assoc, Category.id_comp]
        rw [â† pullback.condition_assoc, pullback.condition_assoc (f := I.glueDataObjÎ¹ i),
          X.homOfLE_Î¹]
      Â· rw [â† cancel_mono (Scheme.Opens.Î¹ _)]
        simp only [Category.assoc, limit.lift_Ï€, PullbackCone.mk_pt, PullbackCone.mk_Ï€_app,
          glueDataT'Aux_snd_Î¹, limit.lift_Ï€_assoc, cospan_left, glueDataT'Aux_fst_assoc,
          glueDataT_fst_assoc, Scheme.homOfLE_Î¹, Category.id_comp]
        rw [pullback.condition_assoc, pullback.condition_assoc, X.homOfLE_Î¹]
  f_open i j := inferInstance

/-- (Implementation) The map from `Spec(ğ’ªâ‚“/I)` to `X`. See `IdealSheafData.subschemeÎ¹` instead. -/
private noncomputable
def gluedTo : I.glueData.glued âŸ¶ X :=
  Multicoequalizer.desc _ _ (fun i â†¦ I.glueDataObjÎ¹ i â‰« i.1.Î¹)
    (by simp [GlueData.diagram, pullback.condition_assoc])

@[reassoc (attr := simp)]
private lemma Î¹_gluedTo (U : X.affineOpens) :
    I.glueData.Î¹ U â‰« I.gluedTo = I.glueDataObjÎ¹ U â‰« U.1.Î¹ :=
  Multicoequalizer.Ï€_desc _ _ _ _ _

@[reassoc (attr := simp)]
private lemma glueDataObjMap_Î¹ (U V : X.affineOpens) (h : U â‰¤ V) :
    I.glueDataObjMap h â‰« I.glueData.Î¹ V = I.glueData.Î¹ U := by
  have : IsIso (X.homOfLE inf_le_left : (U.1 âŠ“ V.1).toScheme âŸ¶ U) :=
    âŸ¨X.homOfLE (by simpa), by simp, by simpâŸ©
  have H : inv (X.homOfLE inf_le_left : (U.1 âŠ“ V.1).toScheme âŸ¶ U) = X.homOfLE (by simpa) := by
    rw [eq_comm, â† hom_comp_eq_id]; simp
  have := I.glueData.glue_condition U V
  simp only [glueData_J, glueData_V, glueData_t, glueData_U, glueData_f] at this
  rw [â† IsIso.inv_comp_eq] at this
  rw [â† Category.id_comp (I.glueData.Î¹ U), â† this]
  simp_rw [â† Category.assoc]
  congr 1
  rw [â† cancel_mono (glueDataObjÎ¹ _ _)]
  simp [pullback_inv_fst_snd_of_right_isIso_assoc, H]

private lemma gluedTo_injective :
    Function.Injective I.gluedTo := by
  intro a b e
  obtain âŸ¨ia, a : I.glueDataObj ia, rflâŸ© :=
    I.glueData.toGlueData.Î¹_jointly_surjective forget a
  obtain âŸ¨ib, b : I.glueDataObj ib, rflâŸ© :=
    I.glueData.toGlueData.Î¹_jointly_surjective forget b
  change I.glueData.Î¹ ia a = I.glueData.Î¹ ib b
  have : (I.glueDataObjÎ¹ ia a).1 = (I.glueDataObjÎ¹ ib b).1 := by
    have : (I.glueData.Î¹ ia â‰« I.gluedTo) a = (I.glueData.Î¹ ib â‰« I.gluedTo) b := e
    rwa [Î¹_gluedTo, Î¹_gluedTo] at this
  obtain âŸ¨f, g, hfg, HâŸ© := exists_basicOpen_le_affine_inter ia.2 ib.2
    (I.glueDataObjÎ¹ ia a).1
      âŸ¨(I.glueDataObjÎ¹ ia a).2, this â–¸ (I.glueDataObjÎ¹ ib b).2âŸ©
  have hmem (W) (hW : W = X.affineBasicOpen g) :
      b âˆˆ Set.range (I.glueDataObjMap (hW.trans_le (X.affineBasicOpen_le g))) := by
    subst hW
    refine (I.opensRange_glueDataObjMap g).ge ?_
    change (I.glueDataObjÎ¹ ib b).1 âˆˆ X.basicOpen g
    rwa [â† this, â† hfg]
  obtain âŸ¨a, rflâŸ© := (I.opensRange_glueDataObjMap f).ge H
  obtain âŸ¨b, rflâŸ© := hmem (X.affineBasicOpen f) (Subtype.ext hfg)
  simp only [glueData_U, â† Scheme.Hom.comp_apply, glueDataObjMap_glueDataObjÎ¹] at this âŠ¢
  simp only [Scheme.affineBasicOpen_coe, Scheme.Hom.comp_base, TopCat.comp_app,
    Scheme.homOfLE_apply, SetLike.coe_eq_coe] at this
  obtain rfl := (I.glueDataObjÎ¹ (X.affineBasicOpen f)).isEmbedding.injective this
  simp only [glueDataObjMap_Î¹]

lemma range_glueDataObjÎ¹_Î¹_eq_support_inter (U : X.affineOpens) :
    Set.range (I.glueDataObjÎ¹ U â‰« U.1.Î¹) = (I.support : Set X) âˆ© U :=
  (I.range_glueDataObjÎ¹_Î¹ U).trans (I.coe_support_inter U).symm

private lemma range_gluedTo :
    Set.range I.gluedTo = I.support := by
  refine subset_antisymm (Set.range_subset_iff.mpr fun x â†¦ ?_) ?_
  Â· obtain âŸ¨ix, x : I.glueDataObj ix, rflâŸ© :=
      I.glueData.toGlueData.Î¹_jointly_surjective forget x
    change (I.glueData.Î¹ _ â‰« I.gluedTo) x âˆˆ I.support
    rw [Î¹_gluedTo]
    exact ((I.range_glueDataObjÎ¹_Î¹_eq_support_inter ix).le âŸ¨_, rflâŸ©).1
  Â· intro x hx
    obtain âŸ¨_, âŸ¨U, hU, rflâŸ©, hxU, -âŸ© :=
      X.isBasis_affineOpens.exists_subset_of_mem_open (Set.mem_univ x) isOpen_univ
    obtain âŸ¨y, rflâŸ© := (I.range_glueDataObjÎ¹_Î¹_eq_support_inter âŸ¨U, hUâŸ©).ge âŸ¨hx, hxUâŸ©
    rw [â† Î¹_gluedTo]
    exact âŸ¨_, rflâŸ©

private lemma range_glueData_Î¹ (U : X.affineOpens) :
    Set.range (Scheme.Hom.toLRSHom' (X := I.glueDataObj U) <|
      I.glueData.Î¹ U).base = (I.gluedTo â»Â¹áµ U : Set I.glueData.glued) := by
  simp only [TopologicalSpace.Opens.map_coe]
  apply I.gluedTo_injective.image_injective
  rw [â† Set.range_comp, â† TopCat.coe_comp, â† Scheme.Hom.comp_base, Î¹_gluedTo,
    range_glueDataObjÎ¹_Î¹, Set.image_preimage_eq_inter_range, range_gluedTo,
    â† coe_support_inter, Set.inter_comm]

/-- (Implementation) identifying `Spec(Î“(X, U)/I(U))` with its image in `Spec(ğ’ªâ‚“/I)`. -/
private noncomputable
def glueDataObjIso (U : X.affineOpens) :
    I.glueDataObj U â‰… I.gluedTo â»Â¹áµ U :=
  IsOpenImmersion.isoOfRangeEq (I.glueData.Î¹ U) (Scheme.Opens.Î¹ _) (by
    simp only [Scheme.Opens.range_Î¹, TopologicalSpace.Opens.map_coe, range_glueData_Î¹])

@[reassoc (attr := simp)]
private lemma glueDataObjIso_hom_Î¹ (U : X.affineOpens) :
    (I.glueDataObjIso U).hom â‰« (I.gluedTo â»Â¹áµ U).Î¹ = I.glueData.Î¹ U :=
  IsOpenImmersion.isoOfRangeEq_hom_fac _ _ _

private lemma glueDataObjIso_hom_restrict (U : X.affineOpens) :
    (I.glueDataObjIso U).hom â‰« I.gluedTo âˆ£_ â†‘U = I.glueDataObjÎ¹ U := by
  rw [â† cancel_mono U.1.Î¹]; simp

private instance : IsPreimmersion I.gluedTo := by
  rw [IsZariskiLocalAtTarget.iff_of_iSup_eq_top (P := @IsPreimmersion)
    _ (iSup_affineOpens_eq_top X)]
  intro U
  rw [â† MorphismProperty.cancel_left_of_respectsIso @IsPreimmersion (I.glueDataObjIso U).hom,
    glueDataObjIso_hom_restrict]
  infer_instance

private instance : QuasiCompact I.gluedTo :=
  âŸ¨fun _ _ â†¦ (Topology.IsClosedEmbedding.isProperMap
    âŸ¨I.gluedTo.isEmbedding, I.range_gluedTo â–¸ I.support.isClosedâŸ©).isCompact_preimageâŸ©

/-- (Implementation) The underlying space of `Spec(ğ’ªâ‚“/I)` is homeomorphic to the support of `I`. -/
private noncomputable
def gluedHomeo : I.glueData.glued â‰ƒâ‚œ I.support :=
  I.gluedTo.isEmbedding.toHomeomorph.trans (.setCongr I.range_gluedTo)

/-- The subscheme associated to an ideal sheaf. -/
noncomputable
def subscheme : Scheme :=
  I.glueData.glued.restrict
    (f := TopCat.ofHom (toContinuousMap I.gluedHomeo.symm))
    I.gluedHomeo.symm.isOpenEmbedding

/-- (Implementation) The isomorphism between the subscheme and the glued scheme. -/
private noncomputable
def subschemeIso : I.subscheme â‰… I.glueData.glued :=
  letI F := I.glueData.glued.ofRestrict (f := TopCat.ofHom (toContinuousMap I.gluedHomeo.symm))
    I.gluedHomeo.symm.isOpenEmbedding
  have : Epi F.base := ConcreteCategory.epi_of_surjective _ I.gluedHomeo.symm.surjective
  letI := IsOpenImmersion.isIso F
  asIso F

/-- The inclusion from the subscheme associated to an ideal sheaf. -/
noncomputable
def subschemeÎ¹ : I.subscheme âŸ¶ X :=
  (I.subschemeIso.hom â‰« I.gluedTo).copyBase Subtype.val <| by
    ext x
    change (I.gluedHomeo (I.gluedHomeo.symm x)).1 = x.1
    rw [I.gluedHomeo.apply_symm_apply]

lemma subschemeÎ¹_apply (x : I.subscheme) : I.subschemeÎ¹ x = x.1 := rfl

private lemma subschemeÎ¹_def : I.subschemeÎ¹ = I.subschemeIso.hom â‰« I.gluedTo :=
  Scheme.Hom.copyBase_eq _ _ _

/-- See `AlgebraicGeometry.Morphisms.ClosedImmersion` for the closed immersion version. -/
instance : IsPreimmersion I.subschemeÎ¹ := by
  rw [subschemeÎ¹_def]
  infer_instance

instance : QuasiCompact I.subschemeÎ¹ := by
  rw [subschemeÎ¹_def]
  infer_instance

@[simp]
lemma range_subschemeÎ¹ : Set.range I.subschemeÎ¹ = I.support := by
  simp [â† range_gluedTo, I.subschemeÎ¹_def, Set.range_comp]

private lemma opensRange_glueData_Î¹_subschemeIso_inv (U : X.affineOpens) :
    (I.glueData.Î¹ U â‰« I.subschemeIso.inv).opensRange = I.subschemeÎ¹ â»Â¹áµ U := by
  ext1
  simp [Set.range_comp, I.range_glueData_Î¹, subschemeÎ¹_def, â† coe_homeoOfIso_symm,
    â† homeoOfIso_symm, â† Homeomorph.coe_symm_toEquiv, â† Set.preimage_equiv_eq_image_symm]

/-- The subscheme associated to an ideal sheaf `I` is covered by `Spec(Î“(X, U)/I(U))`. -/
noncomputable
def subschemeCover : I.subscheme.AffineOpenCover where
  Iâ‚€ := X.affineOpens
  X U := .of <| Î“(X, U) â§¸ I.ideal U
  f U := I.glueData.Î¹ U â‰« I.subschemeIso.inv
  idx x := (X.openCoverOfIsOpenCover _ (iSup_affineOpens_eq_top X)).idx x.1
  covers x := by
    let U := (X.openCoverOfIsOpenCover _ (iSup_affineOpens_eq_top X)).idx x.1
    obtain âŸ¨âŸ¨y, hy : y âˆˆ U.1âŸ©, rfl : y = x.1âŸ© :=
      (X.openCoverOfIsOpenCover _ (iSup_affineOpens_eq_top X)).covers x.1
    exact (I.opensRange_glueData_Î¹_subschemeIso_inv U).ge hy

@[simp]
lemma opensRange_subschemeCover_map (U : X.affineOpens) :
    (I.subschemeCover.f U).opensRange = I.subschemeÎ¹ â»Â¹áµ U :=
  I.opensRange_glueData_Î¹_subschemeIso_inv U

@[simp]
lemma subschemeCover_map_subschemeÎ¹ (U : X.affineOpens) :
    I.subschemeCover.f U â‰« I.subschemeÎ¹ = I.glueDataObjÎ¹ U â‰« U.1.Î¹ := by
  simp [subschemeCover, subschemeÎ¹_def]

/-- `Î“(ğ’ªâ‚“/I, U) â‰… ğ’ªâ‚“(U)/I(U)`. -/
noncomputable
def subschemeObjIso (U : X.affineOpens) :
    Î“(I.subscheme, I.subschemeÎ¹ â»Â¹áµ U) â‰… .of (Î“(X, U) â§¸ I.ideal U) :=
  I.subscheme.presheaf.mapIso (eqToIso (by simp)).op â‰ªâ‰«
    (I.subschemeCover.f U).appIso _ â‰ªâ‰« Scheme.Î“SpecIso (.of (Î“(X, U) â§¸ I.ideal U))

lemma subschemeÎ¹_app (U : X.affineOpens) : I.subschemeÎ¹.app U =
    CommRingCat.ofHom (Ideal.Quotient.mk (I.ideal U)) â‰«
    (I.subschemeObjIso U).inv := by
  have := I.subschemeCover_map_subschemeÎ¹ U
  simp only [glueDataObjÎ¹, Category.assoc, IsAffineOpen.isoSpec_inv_Î¹] at this
  replace this := Scheme.Hom.congr_app this U
  simp only [Hom.comp_base, TopologicalSpace.Opens.map_comp_obj, Hom.comp_app,
    IsAffineOpen.fromSpec_app_self, eqToHom_op, Category.assoc, Hom.naturality_assoc,
    TopologicalSpace.Opens.map_top, â† Î“SpecIso_inv_naturality_assoc] at this
  simp_rw [â† Category.assoc, â† IsIso.comp_inv_eq] at this
  simp only [â† this, â† Functor.map_inv, inv_eqToHom, Category.assoc, eqToHom_unop,
    â† Functor.map_comp, IsIso.Iso.inv_inv, subschemeObjIso, Iso.trans_inv, Functor.mapIso_inv,
    Iso.op_inv, eqToIso.inv, eqToHom_op, Iso.hom_inv_id_assoc, Hom.appIso_inv_naturality_assoc,
    Functor.op_obj, Functor.op_map, unop_comp, unop_inv, Quiver.Hom.unop_op,
    Hom.app_appIso_inv_assoc, TopologicalSpace.Opens.carrier_eq_coe, TopologicalSpace.Opens.map_coe,
    homOfLE_leOfHom]
  convert (Category.comp_id _).symm
  exact CategoryTheory.Functor.map_id _ _

lemma subschemeÎ¹_app_surjective (U : X.affineOpens) :
    Function.Surjective (I.subschemeÎ¹.app U) := by
  rw [I.subschemeÎ¹_app U]
  exact (I.subschemeObjIso U).commRingCatIsoToRingEquiv.symm.surjective.comp
    Ideal.Quotient.mk_surjective

lemma ker_subschemeÎ¹_app (U : X.affineOpens) :
    RingHom.ker (I.subschemeÎ¹.app U).hom = I.ideal U := by
  rw [subschemeÎ¹_app]
  let e : CommRingCat.of (Î“(X, U) â§¸ I.ideal U) â‰… Î“(I.subscheme, I.subschemeÎ¹ â»Â¹áµ U) :=
    (Scheme.Î“SpecIso _).symm â‰ªâ‰« ((I.subschemeCover.f U).appIso _).symm â‰ªâ‰«
      I.subscheme.presheaf.mapIso (eqToIso (by simp)).op
  change RingHom.ker (e.commRingCatIsoToRingEquiv.toRingHom.comp
    (Ideal.Quotient.mk (I.ideal U))) = _
  rw [RingHom.ker_equiv_comp, Ideal.mk_ker]

@[simp]
lemma ker_subschemeÎ¹ : I.subschemeÎ¹.ker = I := by
  ext; simp [ker_subschemeÎ¹_app]

instance : IsEmpty (âŠ¤ : X.IdealSheafData).subscheme := by
  rw [â† (subschemeÎ¹ _).ker_eq_top_iff_isEmpty, ker_subschemeÎ¹]

/-- Given `I â‰¤ J`, this is the map `Spec(Î“(X, U)/J(U)) âŸ¶ Spec(Î“(X, U)/I(U))`. -/
noncomputable
def glueDataObjHom {I J : IdealSheafData X} (h : I â‰¤ J) (U) :
    J.glueDataObj U âŸ¶ I.glueDataObj U :=
  Spec.map (CommRingCat.ofHom (Ideal.Quotient.factor (h U)))

@[reassoc (attr := simp)]
lemma glueDataObjHom_Î¹ {I J : IdealSheafData X} (h : I â‰¤ J) (U) :
    glueDataObjHom h U â‰« I.glueDataObjÎ¹ U = J.glueDataObjÎ¹ U := by
  rw [glueDataObjHom, glueDataObjÎ¹, glueDataObjÎ¹, â† Spec.map_comp_assoc, â† CommRingCat.ofHom_comp,
    Ideal.Quotient.factor_comp_mk]

@[simp]
lemma glueDataObjHom_id {I : IdealSheafData X} (U) :
    glueDataObjHom (le_refl I) U = ğŸ™ _ := by
  rw [â† cancel_mono (I.glueDataObjÎ¹ U)]
  simp

@[reassoc (attr := simp)]
lemma glueDataObjHom_comp {I J K : IdealSheafData X} (hIJ : I â‰¤ J) (hJK : J â‰¤ K) (U) :
    glueDataObjHom hJK U â‰« glueDataObjHom hIJ U = glueDataObjHom (hIJ.trans hJK) U := by
  rw [â† cancel_mono (I.glueDataObjÎ¹ U)]
  simp

/-- The inclusion of ideal sheaf induces an inclusion of subschemes -/
noncomputable
def inclusion {I J : IdealSheafData X} (h : I â‰¤ J) :
    J.subscheme âŸ¶ I.subscheme :=
  J.subschemeCover.openCover.glueMorphisms (fun U â†¦ glueDataObjHom h U â‰« I.subschemeCover.f U)
  (by
    intro U V
    simp only [â† cancel_mono I.subschemeÎ¹, AffineOpenCover.openCover_X, glueDataObjHom_Î¹_assoc,
      AffineOpenCover.openCover_f, Category.assoc, subschemeCover_map_subschemeÎ¹]
    rw [â† subschemeCover_map_subschemeÎ¹, pullback.condition_assoc, subschemeCover_map_subschemeÎ¹])

@[reassoc (attr := simp)]
lemma subSchemeCover_map_inclusion {I J : IdealSheafData X} (h : I â‰¤ J) (U) :
    J.subschemeCover.f U â‰« inclusion h = glueDataObjHom h U â‰« I.subschemeCover.f U :=
  J.subschemeCover.openCover.Î¹_glueMorphisms _ _ _

@[reassoc (attr := simp)]
lemma inclusion_subschemeÎ¹ {I J : IdealSheafData X} (h : I â‰¤ J) :
    inclusion h â‰« I.subschemeÎ¹ = J.subschemeÎ¹ :=
  J.subschemeCover.openCover.hom_ext _ _ fun _ â†¦ by simp

@[simp, reassoc]
lemma inclusion_id (I : IdealSheafData X) :
    inclusion le_rfl = ğŸ™ I.subscheme :=
  I.subschemeCover.openCover.hom_ext _ _ fun _ â†¦ by simp

@[reassoc (attr := simp)]
lemma inclusion_comp {I J K : IdealSheafData X} (hâ‚ : I â‰¤ J) (hâ‚‚ : J â‰¤ K) :
    inclusion hâ‚‚ â‰« inclusion hâ‚ = inclusion (hâ‚.trans hâ‚‚) :=
  K.subschemeCover.openCover.hom_ext _ _ fun _ â†¦ by simp

/-- The functor taking an ideal sheaf to its associated subscheme. -/
@[simps]
noncomputable
def subschemeFunctor (Y : Scheme.{u}) : (IdealSheafData Y)áµ’áµ– â¥¤ Over Y where
  obj I := .mk I.unop.subschemeÎ¹
  map {I J} h := Over.homMk (IdealSheafData.inclusion h.unop.le)

end IdealSheafData

noncomputable section image

open Limits

variable {X Y : Scheme.{u}} (f : X âŸ¶ Y) (U : Y.affineOpens)

/-- The scheme-theoretic image of a morphism. -/
abbrev Hom.image : Scheme.{u} := f.ker.subscheme

/-- The embedding from the scheme-theoretic image to the codomain. -/
abbrev Hom.imageÎ¹ : f.image âŸ¶ Y := f.ker.subschemeÎ¹

lemma ideal_ker_le_ker_Î“SpecIso_inv_comp :
    f.ker.ideal U â‰¤ RingHom.ker ((Î“SpecIso Î“(Y, â†‘U)).inv â‰«
      (pullback.snd f U.1.Î¹ â‰« U.1.toSpecÎ“).appTop).hom := by
  let e : Î“(X, f â»Â¹áµ â†‘U) â‰… Î“(Limits.pullback (C := Scheme) f U.1.Î¹, âŠ¤) :=
    X.presheaf.mapIso (eqToIso (by simp [Scheme.Hom.opensRange_pullbackFst])).op
      â‰ªâ‰« (Limits.pullback.fst (C := Scheme) f U.1.Î¹).appIso âŠ¤
  have he : f.app U â‰« e.hom =
      (Î“SpecIso Î“(Y, â†‘U)).inv â‰« (pullback.snd f U.1.Î¹ â‰« U.1.toSpecÎ“).appTop := by
    rw [â† (Iso.inv_comp_eq _).mpr U.2.isoSpec_inv_appTop, Category.assoc, Iso.eq_inv_comp]
    simp only [Opens.topIso_hom, eqToHom_op, Hom.app_eq_appLE, Iso.trans_hom, Functor.mapIso_hom,
      Iso.op_hom, eqToIso.hom, Hom.appIso_hom, Hom.appLE_map, Hom.map_appLE, Hom.appLE_comp_appLE,
      Opens.map_top, e, pullback.condition, IsAffineOpen.toSpecÎ“_isoSpec_inv, Category.assoc]
    rw [Hom.comp_appLE, Opens.Î¹_app]
    exact Hom.map_appLE _ _ (homOfLE le_top).op
  rw [â† he]
  refine (IdealSheafData.ideal_ofIdeals_le _ _).trans_eq
    (RingHom.ker_equiv_comp _ e.commRingCatIsoToRingEquiv).symm

private noncomputable
def Hom.toImageAux : X âŸ¶ f.image :=
  Cover.glueMorphisms ((Y.openCoverOfIsOpenCover _ (iSup_affineOpens_eq_top Y)).pullbackâ‚ f)
    (fun U â†¦ (pullback.snd f U.1.Î¹ â‰« U.1.toSpecÎ“).liftQuotient _
      (by exact ideal_ker_le_ker_Î“SpecIso_inv_comp f U) â‰« f.ker.subschemeCover.f U) (by
    intro U V
    rw [â† cancel_mono f.imageÎ¹]
    simp [IdealSheafData.glueDataObjÎ¹, Scheme.Hom.liftQuotient_comp_assoc,
      â† pullback.condition, â† pullback.condition_assoc])

private lemma Hom.toImageAux_spec :
    f.toImageAux â‰« f.imageÎ¹ = f := by
  apply Cover.hom_ext ((Y.openCoverOfIsOpenCover _ (iSup_affineOpens_eq_top Y)).pullbackâ‚ f)
  intro U
  simp only [Hom.toImageAux, Cover.Î¹_glueMorphisms_assoc]
  simp [IdealSheafData.glueDataObjÎ¹, Scheme.Hom.liftQuotient_comp_assoc, pullback.condition]

/-- The morphism from the domain to the scheme-theoretic image. -/
noncomputable
def Hom.toImage : X âŸ¶ f.image :=
  f.toImageAux.copyBase (fun x â†¦ âŸ¨f x, f.range_subset_ker_support âŸ¨x, rflâŸ©âŸ©)
    (funext fun x â†¦ Subtype.ext congr($f.toImageAux_spec x))

@[reassoc (attr := simp)]
lemma Hom.toImage_imageÎ¹ :
    f.toImage â‰« f.imageÎ¹ = f := by
  convert f.toImageAux_spec using 2
  exact Scheme.Hom.copyBase_eq _ _ _

instance [QuasiCompact f] : IsDominant f.toImage where
  denseRange := by
    rw [denseRange_iff_closure_range, f.imageÎ¹.isEmbedding.closure_eq_preimage_closure_image,
      â† Set.univ_subset_iff, â† Set.image_subset_iff, Set.image_univ,
      IdealSheafData.range_subschemeÎ¹, Hom.support_ker, â† Set.range_comp,
      â† TopCat.coe_comp, â† Scheme.Hom.comp_base, f.toImage_imageÎ¹]

instance [QuasiCompact f] : QuasiCompact f.toImage :=
  have : QuasiCompact (f.toImage â‰« f.imageÎ¹) := by simpa
  .of_comp _ f.imageÎ¹

instance : IsIso (IdealSheafData.subschemeÎ¹ âŠ¥ : _ âŸ¶ X) :=
  âŸ¨Scheme.Hom.toImage (ğŸ™ X) â‰« IdealSheafData.inclusion bot_le,
    by simp [â† cancel_mono (IdealSheafData.subschemeÎ¹ _)], by simpâŸ©

lemma Hom.toImage_app :
    f.toImage.app (f.imageÎ¹ â»Â¹áµ U) =
      (f.ker.subschemeObjIso U).hom â‰« CommRingCat.ofHom
        (Ideal.Quotient.lift _ (f.app U.1).hom (IdealSheafData.ideal_ofIdeals_le _ _)) := by
  have := ConcreteCategory.epi_of_surjective _ (f.ker.subschemeÎ¹_app_surjective U)
  rw [â† cancel_epi (f.ker.subschemeÎ¹.app U), â† Scheme.Hom.comp_app,
    Scheme.Hom.congr_app f.toImage_imageÎ¹, f.ker.subschemeÎ¹_app,
    â† IsIso.eq_comp_inv, â† Functor.map_inv]
  simp only [Hom.comp_base, Opens.map_comp_obj, Category.assoc,
    Iso.inv_hom_id_assoc, eqToHom_op, inv_eqToHom]
  rw [â† reassoc_of% CommRingCat.ofHom_comp, Ideal.Quotient.lift_comp_mk, CommRingCat.ofHom_hom,
    eqToHom_refl, CategoryTheory.Functor.map_id]
  exact (Category.comp_id _).symm

lemma Hom.toImage_app_injective [QuasiCompact f] :
    Function.Injective (f.toImage.app (f.imageÎ¹ â»Â¹áµ U)) := by
  simp only [f.toImage_app U, CommRingCat.hom_comp, CommRingCat.hom_ofHom, RingHom.coe_comp]
  exact (RingHom.lift_injective_of_ker_le_ideal _ _ (by simp)).comp
    (f.ker.subschemeObjIso U).commRingCatIsoToRingEquiv.injective

lemma Hom.stalkFunctor_toImage_injective [QuasiCompact f] (x) :
    Function.Injective ((TopCat.Presheaf.stalkFunctor _ x).map f.toImage.c) := by
  apply TopCat.Presheaf.stalkFunctor_map_injective_of_isBasis
    (hB := (Y.isBasis_affineOpens.of_isInducing f.imageÎ¹.isEmbedding.isInducing))
  rintro _ âŸ¨U, hU, rflâŸ©
  exact f.toImage_app_injective âŸ¨U, hUâŸ©

open IdealSheafData in
/-- The adjunction between `Y.IdealSheafData` and `(Over Y)áµ’áµ–` given by taking kernels. -/
@[simps]
noncomputable
def kerAdjunction (Y : Scheme.{u}) : (subschemeFunctor Y).rightOp âŠ£ Y.kerFunctor where
  unit.app I := eqToHom (by simp)
  counit.app f := (Over.homMk f.unop.hom.toImage f.unop.hom.toImage_imageÎ¹).op
  counit.naturality _ _ _ := Quiver.Hom.unop_inj (by ext1; simp [â† cancel_mono (subschemeÎ¹ _)])
  left_triangle_components I := Quiver.Hom.unop_inj (by ext1; simp [â† cancel_mono (subschemeÎ¹ _)])

instance : (IdealSheafData.subschemeFunctor Y).Full :=
  have : IsIso Y.kerAdjunction.rightOp.counit := by
    simp [NatTrans.isIso_iff_isIso_app, CategoryTheory.instIsIsoEqToHom]
  Y.kerAdjunction.rightOp.fullyFaithfulROfIsIsoCounit.full

end image

end Scheme

end AlgebraicGeometry
