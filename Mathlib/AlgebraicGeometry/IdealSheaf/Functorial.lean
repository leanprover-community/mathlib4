/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.ClosedImmersion
import Mathlib.AlgebraicGeometry.PullbackCarrier

/-!
# Functorial constructions of ideal sheaves

We define the pullback and pushforward of ideal sheaves in this file.

## Main definitions
- `AlgebraicGeometry.Scheme.IdealSheafData.comap`: The pullback of an ideal sheaf.
- `AlgebraicGeometry.Scheme.IdealSheafData.map`: The pushforward of an ideal sheaf.
- `AlgebraicGeometry.Scheme.IdealSheafData.map_gc`:
  The galois connection between pullback and pushforward.

-/

noncomputable section

universe u

open CategoryTheory Limits

namespace AlgebraicGeometry

variable {X Y Z : Scheme.{u}}

namespace Scheme.IdealSheafData

/-- The pullback of an ideal sheaf. -/
def comap (I : Y.IdealSheafData) (f : X âŸ¶ Y) : X.IdealSheafData :=
  (pullback.fst f I.subschemeÎ¹).ker

/-- The subscheme associated to the pullback ideal sheaf is isomorphic to the fibred product. -/
def comapIso (I : Y.IdealSheafData) (f : X âŸ¶ Y) :
    (I.comap f).subscheme â‰… pullback f I.subschemeÎ¹ :=
  (asIso (pullback.fst f I.subschemeÎ¹).toImage).symm

@[reassoc (attr := simp)]
lemma comapIso_inv_subschemeÎ¹ (I : Y.IdealSheafData) (f : X âŸ¶ Y) :
    (I.comapIso f).inv â‰« (I.comap f).subschemeÎ¹ = pullback.fst _ _ :=
  (pullback.fst f I.subschemeÎ¹).toImage_imageÎ¹

@[reassoc (attr := simp)]
lemma comapIso_hom_fst (I : Y.IdealSheafData) (f : X âŸ¶ Y) :
    (I.comapIso f).hom â‰« pullback.fst _ _ = (I.comap f).subschemeÎ¹ := by
  rw [â† comapIso_inv_subschemeÎ¹, Iso.hom_inv_id_assoc]

@[simp]
lemma comap_comp (I : Z.IdealSheafData) (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    I.comap (f â‰« g) = (I.comap g).comap f := by
  let e : pullback f (I.comap g).subschemeÎ¹ â‰… pullback (f â‰« g) I.subschemeÎ¹ :=
    asIso (pullback.map _ _ _ _ (ğŸ™ _) (I.comapIso g).hom (ğŸ™ _) (by simp) (by simp)) â‰ªâ‰«
      pullbackRightPullbackFstIso _ _ _
  rw [comap, comap, â† Scheme.Hom.ker_comp_of_isIso e.hom]
  simp [e]

@[simp]
lemma comap_id (I : Z.IdealSheafData) :
    I.comap (ğŸ™ _) = I := by
  rw [comap, â† Scheme.Hom.ker_comp_of_isIso (inv (pullback.snd _ _)),
    pullback_inv_snd_fst_of_left_isIso, IsIso.inv_id, Category.comp_id, ker_subschemeÎ¹]

@[simp]
lemma support_comap (I : Y.IdealSheafData) (f : X âŸ¶ Y) :
    (I.comap f).support = I.support.preimage f.continuous := by
  ext1
  rw [comap, Scheme.Hom.support_ker, Pullback.range_fst, range_subschemeÎ¹,
    TopologicalSpace.Closeds.coe_preimage, (I.support.isClosed.preimage f.continuous).closure_eq]

/-- The pushforward of an ideal sheaf. -/
def map (I : X.IdealSheafData) (f : X âŸ¶ Y) : Y.IdealSheafData :=
  (I.subschemeÎ¹ â‰« f).ker

lemma le_map_iff_comap_le {I : X.IdealSheafData} {f : X âŸ¶ Y} {J : Y.IdealSheafData} :
    J â‰¤ I.map f â†” J.comap f â‰¤ I := by
  constructor
  Â· intro H
    rw [â† I.ker_subschemeÎ¹, â† pullback.lift_fst (f := f) (g := J.subschemeÎ¹) I.subschemeÎ¹
      ((I.subschemeÎ¹ â‰« f).toImage â‰« inclusion H) (by simp)]
    exact Hom.le_ker_comp _ _
  Â· intro H
    have : (inclusion H â‰« (J.comapIso f).hom â‰« pullback.snd _ _) â‰« J.subschemeÎ¹ =
        I.subschemeÎ¹ â‰« f := by simp [â† pullback.condition]
    rw [map, â† J.ker_subschemeÎ¹, â† this]
    exact Hom.le_ker_comp _ _

section gc

variable (I Iâ‚ Iâ‚‚ : X.IdealSheafData) (J Jâ‚ Jâ‚‚ : Y.IdealSheafData) (f : X âŸ¶ Y)

/-- Pushforward and pullback of ideal sheaves forms a galois connection. -/
lemma map_gc : GaloisConnection (comap Â· f) (map Â· f) := fun _ _ â†¦ le_map_iff_comap_le.symm

lemma map_mono          : Monotone (map Â· f)                          := (map_gc f).monotone_u
lemma comap_mono        : Monotone (comap Â· f)                        := (map_gc f).monotone_l
lemma le_map_comap      : J â‰¤ (J.comap f).map f                       := (map_gc f).le_u_l J
lemma comap_map_le      : (I.map f).comap f â‰¤ I                       := (map_gc f).l_u_le I
@[simp] lemma map_top   : map âŠ¤ f = âŠ¤                                 := (map_gc f).u_top
@[simp] lemma comap_bot : comap âŠ¥ f = âŠ¥                               := (map_gc f).l_bot
@[simp] lemma map_inf   : map (Iâ‚ âŠ“ Iâ‚‚) f = map Iâ‚ f âŠ“ map Iâ‚‚ f       := (map_gc f).u_inf
@[simp] lemma comap_sup : comap (Jâ‚ âŠ” Jâ‚‚) f = comap Jâ‚ f âŠ” comap Jâ‚‚ f := (map_gc f).l_sup

end gc

@[simp]
lemma map_bot (f : X âŸ¶ Y) : map âŠ¥ f = f.ker := by
  simp [map, Scheme.Hom.ker_comp_of_isIso]

@[simp]
lemma comap_top (f : X âŸ¶ Y) : comap âŠ¤ f = âŠ¤ := by
  rw [comap, Hom.ker_eq_top_iff_isEmpty]
  exact Function.isEmpty (pullback.snd f _).base

@[simp]
lemma map_comp (I : X.IdealSheafData) (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    I.map (f â‰« g) = (I.map f).map g := by
  apply le_antisymm
  Â· rw [le_map_iff_comap_le, le_map_iff_comap_le, â† comap_comp]; exact comap_map_le _ _
  Â· rw [le_map_iff_comap_le, comap_comp]
    exact (comap_mono _ (comap_map_le _ _)).trans (comap_map_le _ _)

@[simp]
lemma map_id (I : Z.IdealSheafData) :
    I.map (ğŸ™ _) = I := by
  simp [map]

lemma map_ker (f : X âŸ¶ Y) (g : Y âŸ¶ Z) : f.ker.map g = (f â‰« g).ker := by
  simp [â† map_bot]

lemma _root_.AlgebraicGeometry.Scheme.Hom.ker_comp
    (f : X âŸ¶ Y) (g : Y âŸ¶ Z) : (f â‰« g).ker = f.ker.map g := (map_ker f g).symm

lemma map_vanishingIdeal {X Y : Scheme} (f : X âŸ¶ Y) (Z : TopologicalSpace.Closeds X) :
    (vanishingIdeal Z).map f = vanishingIdeal (.closure (f.base '' Z)) := by
  apply le_antisymm
  Â· rw [map, â† le_support_iff_le_vanishingIdeal, TopologicalSpace.Closeds.closure_le]
    refine .trans ?_ (Hom.range_subset_ker_support _)
    rw [Scheme.comp_base, TopCat.coe_comp, Set.range_comp,
      range_subschemeÎ¹, coe_support_vanishingIdeal]
  Â· simp [le_map_iff_comap_le, â† le_support_iff_le_vanishingIdeal, â† Set.image_subset_iff,
      subset_closure, â† SetLike.coe_subset_coe]

@[simp]
lemma support_map (I : X.IdealSheafData) (f : X âŸ¶ Y) [QuasiCompact f] :
    (I.map f).support = .closure (f.base '' I.support) := by
  ext1
  rw [map, Scheme.Hom.support_ker, Scheme.comp_base, TopCat.coe_comp,
    Set.range_comp, range_subschemeÎ¹, TopologicalSpace.Closeds.coe_closure]

lemma ideal_map (I : X.IdealSheafData) (f : X âŸ¶ Y) [QuasiCompact f] (U : Y.affineOpens)
    (H : IsAffineOpen (f â»Â¹áµ U)) :
    (I.map f).ideal U = (I.ideal âŸ¨_, HâŸ©).comap (f.app U).hom := by
  have : RingHom.ker (I.subschemeObjIso âŸ¨_, HâŸ©).inv.hom = âŠ¥ :=
    RingHom.ker_coe_equiv (I.subschemeObjIso âŸ¨_, HâŸ©).symm.commRingCatIsoToRingEquiv
  simp [map, â† RingHom.comap_ker, subschemeÎ¹_app _ âŸ¨_, HâŸ©,
    this, â† RingHom.ker_eq_comap_bot]

lemma ideal_map_of_isAffineHom
    (I : X.IdealSheafData) (f : X âŸ¶ Y) [IsAffineHom f] (U : Y.affineOpens) :
    (I.map f).ideal U = (I.ideal âŸ¨_, U.2.preimage fâŸ©).comap (f.app U).hom :=
  ideal_map I f U (U.2.preimage f)

lemma ideal_comap_of_isOpenImmersion
    (I : Y.IdealSheafData) (f : X âŸ¶ Y) [IsOpenImmersion f] (U : X.affineOpens) :
    (I.comap f).ideal U = (I.ideal âŸ¨f ''áµ U, U.2.image_of_isOpenImmersion fâŸ©).comap
      (f.appIso U).inv.hom := by
  refine (ker_ideal_of_isPullback_of_isOpenImmersion _ _ _ _
    (IsPullback.of_hasPullback f I.subschemeÎ¹) U).trans ?_
  simp

/-- If `J â‰¤ I.map f`, then `f` restricts to a map `I âŸ¶ J` between the closed subschemes. -/
def subschemeMap (I : X.IdealSheafData) (J : Y.IdealSheafData)
    (f : X âŸ¶ Y) (H : J â‰¤ I.map f) : I.subscheme âŸ¶ J.subscheme :=
  IsClosedImmersion.lift J.subschemeÎ¹ (I.subschemeÎ¹ â‰« f) (by simpa using H)

@[reassoc (attr := simp)]
lemma subschemeMap_subschemeÎ¹ (I : X.IdealSheafData) (J : Y.IdealSheafData)
    (f : X âŸ¶ Y) (H : J â‰¤ I.map f) : subschemeMap I J f H â‰« J.subschemeÎ¹ = I.subschemeÎ¹ â‰« f :=
  IsClosedImmersion.lift_fac _ _ _

@[reassoc (attr := simp)]
lemma comapIso_hom_snd (I : Y.IdealSheafData) (f : X âŸ¶ Y) :
    (I.comapIso f).hom â‰« pullback.snd _ _ = subschemeMap _ _ f (I.le_map_comap f) := by
  rw [â† cancel_mono I.subschemeÎ¹]
  simp [â† pullback.condition]

end Scheme.IdealSheafData

end AlgebraicGeometry
