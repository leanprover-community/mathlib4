/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Category.ModuleCat.Sheaf.Abelian
import Mathlib.Algebra.Category.ModuleCat.Sheaf.PullbackContinuous
import Mathlib.AlgebraicGeometry.Modules.Presheaf
import Mathlib.CategoryTheory.Bicategory.Adjunction.Adj
import Mathlib.CategoryTheory.Bicategory.Adjunction.Cat
import Mathlib.CategoryTheory.Bicategory.Functor.LocallyDiscrete

/-!
# The category of sheaves of modules over a scheme

In this file, we define the abelian category of sheaves of modules
`X.Modules` over a scheme `X`, and study its basic functoriality.

-/

universe t u

open CategoryTheory Bicategory

-- to be moved...
namespace TopologicalSpace.Opens

instance {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]
    (F : Opens X ‚•§ Opens Y) (G : Opens Y ‚•§ Opens Z)
    [Functor.IsContinuous.{t} F (Opens.grothendieckTopology _) (Opens.grothendieckTopology _)]
    [Functor.IsContinuous.{t} G (Opens.grothendieckTopology _) (Opens.grothendieckTopology _)] :
    Functor.IsContinuous.{t} (F ‚ãô G) (Opens.grothendieckTopology _)
      (Opens.grothendieckTopology _) :=
  Functor.isContinuous_comp _ _ _ (Opens.grothendieckTopology _) _

end TopologicalSpace.Opens

-- to be moved
namespace AlgebraicGeometry.LocallyRingedSpace

variable {X Y : LocallyRingedSpace} (f : X.Hom Y)

/-- The morphism of sheaves corresponding to a morphism of locally ringed spaces. -/
abbrev Hom.toSheafHom :
    Y.sheaf ‚ü∂ ((TopologicalSpace.Opens.map f.base).sheafPushforwardContinuous
      _ _ _).obj X.sheaf where
  val := f.c

end AlgebraicGeometry.LocallyRingedSpace

namespace AlgebraicGeometry.Scheme

variable {X Y Z T : Scheme.{u}}

variable (X) in
/-- The category of sheaves of modules over a scheme. -/
abbrev Modules := SheafOfModules.{u} X.ringCatSheaf

example : HasSheafify (Opens.grothendieckTopology X) AddCommGrpCat.{u} :=
  inferInstance

instance : Abelian X.Modules := inferInstance

/-- The morphism of sheaves of rings corresponding to a morphism of schemes. -/
def Hom.toRingCatSheafHom (f : X ‚ü∂ Y) :
    Y.ringCatSheaf ‚ü∂ ((TopologicalSpace.Opens.map f.base).sheafPushforwardContinuous
      _ _ _).obj X.ringCatSheaf :=
  (sheafCompose _ (forget‚ÇÇ _ RingCat)).map f.toSheafHom

namespace Modules

variable (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (h : Z ‚ü∂ T)

/-- The pushforward functor for categories of sheaves of modules over schemes. -/
noncomputable def pushforward : X.Modules ‚•§ Y.Modules :=
  SheafOfModules.pushforward f.toRingCatSheafHom

/-- The pullback functor for categories of sheaves of modules over schemes. -/
noncomputable def pullback : Y.Modules ‚•§ X.Modules :=
  SheafOfModules.pullback f.toRingCatSheafHom

/-- The pullback functor for categories of sheaves of modules over schemes
is left adjoint to the pushforward functor. -/
noncomputable def pullbackPushforwardAdjunction : pullback f ‚ä£ pushforward f :=
  SheafOfModules.pullbackPushforwardAdjunction _

variable (X) in
/-- The pushforward of sheaves of modules by the identity morphism identifies
to the identity functor. -/
noncomputable def pushforwardId : pushforward (ùüô X) ‚âÖ ùü≠ _ :=
  SheafOfModules.pushforwardId _

variable (X) in
/-- The pullback of sheaves of modules by the identity morphism identifies
to the identity functor. -/
noncomputable def pullbackId : pullback (ùüô X) ‚âÖ ùü≠ _ :=
  SheafOfModules.pullbackId _

variable (X) in
@[simp]
lemma conjugateEquiv_pullbackId_hom :
    conjugateEquiv .id (pullbackPushforwardAdjunction (ùüô X)) (pullbackId X).hom =
      (pushforwardId X).inv :=
  SheafOfModules.conjugateEquiv_pullbackId_hom _

/-- The composition of two pushforward functors for sheaves of modules on schemes
identify to the pushforward for the composition. -/
noncomputable def pushforwardComp :
    pushforward f ‚ãô pushforward g ‚âÖ pushforward (f ‚â´ g) :=
  SheafOfModules.pushforwardComp _ _

/-- The composition of two pullback functors for sheaves of modules on schemes
identify to the pullback for the composition. -/
noncomputable def pullbackComp :
    pullback g ‚ãô pullback f ‚âÖ pullback (f ‚â´ g) :=
  SheafOfModules.pullbackComp _ _

@[simp]
lemma conjugateEquiv_pullbackComp_inv :
    conjugateEquiv ((pullbackPushforwardAdjunction g).comp (pullbackPushforwardAdjunction f))
      (pullbackPushforwardAdjunction (f ‚â´ g)) (pullbackComp f g).inv =
    (pushforwardComp f g).hom :=
  SheafOfModules.conjugateEquiv_pullbackComp_inv _ _

@[reassoc]
lemma pseudofunctor_associativity :
    (pullbackComp f (g ‚â´ h)).inv ‚â´
      Functor.whiskerRight (pullbackComp g h).inv _ ‚â´ (Functor.associator _ _ _).hom ‚â´
        Functor.whiskerLeft _ (pullbackComp f g).hom ‚â´ (pullbackComp (f ‚â´ g) h).hom =
    eqToHom (by simp) := by
  let e‚ÇÅ := pullbackComp f (g ‚â´ h)
  let e‚ÇÇ := Functor.isoWhiskerRight (pullbackComp g h) (pullback f)
  let e‚ÇÉ := Functor.isoWhiskerLeft (pullback h) (pullbackComp f g)
  let e‚ÇÑ := pullbackComp (f ‚â´ g) h
  change e‚ÇÅ.inv ‚â´ e‚ÇÇ.inv ‚â´ (Functor.associator _ _ _).hom ‚â´ e‚ÇÉ.hom ‚â´ e‚ÇÑ.hom = _
  have : e‚ÇÉ.hom ‚â´ e‚ÇÑ.hom = (Functor.associator _ _ _).inv ‚â´ e‚ÇÇ.hom ‚â´ e‚ÇÅ.hom :=
    congr_arg Iso.hom (SheafOfModules.pullback_assoc.{u}
      h.toRingCatSheafHom g.toRingCatSheafHom f.toRingCatSheafHom)
  simp [this]

@[reassoc]
lemma pseudofunctor_left_unitality :
    (pullbackComp f (ùüô Y)).inv ‚â´
      Functor.whiskerRight (pullbackId Y).hom (pullback f) ‚â´ (Functor.leftUnitor _).hom =
        eqToHom (by simp) := by
  let e‚ÇÅ := pullbackComp f (ùüô _)
  let e‚ÇÇ := Functor.isoWhiskerRight (pullbackId Y) (pullback f)
  let e‚ÇÉ := (pullback f).leftUnitor
  change e‚ÇÅ.inv ‚â´ e‚ÇÇ.hom ‚â´ e‚ÇÉ.hom = _
  have : e‚ÇÅ.hom = e‚ÇÇ.hom ‚â´ e‚ÇÉ.hom :=
    congr_arg Iso.hom (SheafOfModules.pullback_id_comp.{u} f.toRingCatSheafHom)
  simp [‚Üê this]

@[reassoc]
lemma pseudofunctor_right_unitality :
    (pullbackComp (ùüô X) f).inv ‚â´
      Functor.whiskerLeft (pullback f) (pullbackId X).hom ‚â´ (Functor.rightUnitor _).hom =
        eqToHom (by simp) := by
  let e‚ÇÅ := pullbackComp (ùüô _) f
  let e‚ÇÇ := Functor.isoWhiskerLeft (pullback f) (pullbackId _)
  let e‚ÇÉ := (pullback f).rightUnitor
  change e‚ÇÅ.inv ‚â´ e‚ÇÇ.hom ‚â´ e‚ÇÉ.hom = _
  have : e‚ÇÅ.hom = e‚ÇÇ.hom ‚â´ e‚ÇÉ.hom :=
    congr_arg Iso.hom (SheafOfModules.pullback_comp_id.{u} f.toRingCatSheafHom)
  simp [‚Üê this]

/-- The pseudofunctor from `Scheme·µí·µñ` to the bicategory of adjunctions which sends
a scheme `X` to the category `X.Modules` of sheaves of modules over `X`.
(This contains both the covariant and the contravariant functorialities of
these categories.) -/
noncomputable def pseudofunctor :
    Pseudofunctor (LocallyDiscrete Scheme.{u}·µí·µñ) (Adj Cat) :=
  LocallyDiscrete.mkPseudofunctor
    (fun X ‚Ü¶ Adj.mk (Cat.of X.unop.Modules))
    (fun {Y X} f ‚Ü¶ .mk (pullbackPushforwardAdjunction f.unop).toCat)
    (fun X ‚Ü¶ Adj.iso‚ÇÇMk (pullbackId _) (pushforwardId _).symm (by
      dsimp
      rw [Bicategory.conjugateEquiv_eq_categoryTheoryConjugateEquiv]
      apply conjugateEquiv_pullbackId_hom))
    (fun {Z Y X} f g ‚Ü¶ Adj.iso‚ÇÇMk (pullbackComp _ _).symm (pushforwardComp _ _) (by
      dsimp
      rw [Bicategory.conjugateEquiv_eq_categoryTheoryConjugateEquiv,
        Adjunction.toCat_comp_toCat]
      apply conjugateEquiv_pullbackComp_inv))
    (fun _ _ _ ‚Ü¶ by ext : 1; apply pseudofunctor_associativity _ _ _)
    (fun _ ‚Ü¶ by ext : 1; apply pseudofunctor_left_unitality)
    (fun _ ‚Ü¶ by ext : 1; apply pseudofunctor_right_unitality)

attribute [simps! obj_obj map_l map_r map_adj] pseudofunctor
attribute [simps! mapId_hom_œÑl mapId_hom_œÑr mapId_inv_œÑl mapId_inv_œÑr] pseudofunctor
attribute [simps! mapComp_hom_œÑl mapComp_hom_œÑr] pseudofunctor
attribute [simps! mapComp_inv_œÑl mapComp_inv_œÑr] pseudofunctor

end Modules

end AlgebraicGeometry.Scheme
