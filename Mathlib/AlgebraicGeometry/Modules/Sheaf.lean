/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou, Andrew Yang
-/
module

public import Mathlib.Algebra.Category.ModuleCat.Sheaf.Abelian
public import Mathlib.Algebra.Category.ModuleCat.Sheaf.Colimits
public import Mathlib.Algebra.Category.ModuleCat.Sheaf.PullbackContinuous
public import Mathlib.AlgebraicGeometry.Modules.Presheaf
public import Mathlib.AlgebraicGeometry.OpenImmersion
public import Mathlib.CategoryTheory.Bicategory.Adjunction.Adj
public import Mathlib.CategoryTheory.Bicategory.Adjunction.Cat
public import Mathlib.CategoryTheory.Bicategory.Functor.LocallyDiscrete

/-!
# The category of sheaves of modules over a scheme

In this file, we define the abelian category of sheaves of modules
`X.Modules` over a scheme `X`, and study its basic functoriality.

-/

@[expose] public section

universe t u

open CategoryTheory Limits TopologicalSpace SheafOfModules Bicategory

namespace AlgebraicGeometry.Scheme

variable {X Y Z T : Scheme.{u}} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)

variable (X) in
/-- The category of sheaves of modules over a scheme. -/
def Modules := SheafOfModules.{u} X.ringCatSheaf

namespace Modules

/-- Morphisms between `ğ’ªâ‚“`-modules. Use `Hom.app` to act on sections. -/
def Hom (M N : X.Modules) : Type u := SheafOfModules.Hom M N

instance : Category X.Modules where
  Hom := Modules.Hom
  __ := inferInstanceAs (Category (SheafOfModules.{u} X.ringCatSheaf))

instance : Abelian X.Modules := inferInstanceAs (Abelian (SheafOfModules.{u} X.ringCatSheaf))
instance : HasLimits X.Modules := inferInstanceAs (HasLimits (SheafOfModules X.ringCatSheaf))
instance : HasColimits X.Modules := inferInstanceAs (HasColimits (SheafOfModules X.ringCatSheaf))

section Functor

variable (X) in
/-- The forgetful functor from `ğ’ªâ‚“`-modules to presheaves of modules.
This is mostly useful to transport results from (pre)sheaves of modules to `ğ’ªâ‚“`-modules and
usually shouldn't be used directly when working with actual `ğ’ªâ‚“`-modules. -/
def toPresheafOfModules : X.Modules â¥¤ X.PresheafOfModules := SheafOfModules.forget _

/-- The forgetful functor from `ğ’ªâ‚“`-modules to presheaves of modules is fully faithful. -/
def fullyFaithfulToPresheafOfModules : (Modules.toPresheafOfModules X).FullyFaithful :=
  SheafOfModules.fullyFaithfulForget _

instance : (toPresheafOfModules X).Full := fullyFaithfulToPresheafOfModules.full
instance : (toPresheafOfModules X).Faithful := fullyFaithfulToPresheafOfModules.faithful
instance : (toPresheafOfModules X).IsRightAdjoint :=
  (PresheafOfModules.sheafificationAdjunction (ğŸ™ X.ringCatSheaf.val)).isRightAdjoint

variable (X) in
/-- The forgetful functor from `ğ’ªâ‚“`-modules to presheaves of abelian groups. -/
noncomputable def toPresheaf : X.Modules â¥¤ TopCat.Presheaf Ab X :=
  toPresheafOfModules X â‹™ PresheafOfModules.toPresheaf _

instance : (toPresheaf X).Faithful := .comp _ (PresheafOfModules.toPresheaf _)
instance : PreservesLimits (toPresheaf X) := comp_preservesLimits _ (PresheafOfModules.toPresheaf _)
instance : (toPresheaf X).ReflectsIsomorphisms :=
  reflectsIsomorphisms_comp _ (PresheafOfModules.toPresheaf _)

end Functor

variable {M N K : X.Modules} {Ï† : M âŸ¶ N} {U V : X.Opens}

section Presheaf

/-- The underlying abelian presheaf of an `ğ’ªâ‚“`-module. -/
noncomputable def presheaf (M : X.Modules) : TopCat.Presheaf Ab X := M.1.presheaf

/-- Notation for sections of a presheaf of module. -/
scoped[AlgebraicGeometry] notation3 "Î“(" M ", " U ")" => (Scheme.Modules.presheaf M).obj (.op U)

instance : Module Î“(X, U) Î“(M, U) := (M.val.obj (.op U)).isModule

variable (M) in
@[simp] lemma map_smul (i : U âŸ¶ V) (r : Î“(X, V)) (x : Î“(M, V)) :
    M.presheaf.map i.op (r â€¢ x) = X.presheaf.map i.op r â€¢ M.presheaf.map i.op x :=
  M.val.map_smul _ _ _

/-- The underlying map between abelian presheaves of a morphism of `ğ’ªâ‚“`-modules. -/
noncomputable def Hom.mapPresheaf (Ï† : M âŸ¶ N) : M.presheaf âŸ¶ N.presheaf :=
  (toPresheaf X).map Ï†

/-- The application of a morphism of `ğ’ªâ‚“`-modules to sections. -/
def Hom.app (Ï† : M âŸ¶ N) (U : X.Opens) : Î“(M, U) âŸ¶ Î“(N, U) :=
  (forgetâ‚‚ _ _).map (Ï†.val.app (.op U))

@[simp] lemma mapPresheaf_app (Ï† : M âŸ¶ N) (U) : Ï†.mapPresheaf.app U = Ï†.app U.unop := rfl

@[simp]
lemma Hom.app_smul (Ï† : M âŸ¶ N) (r : Î“(X, U)) (x : Î“(M, U)) :
    Ï†.app U (r â€¢ x) = r â€¢ Ï†.app U x :=
  (Ï†.val.app (.op U)).hom.map_smul r x

@[simp] lemma Hom.add_app (Ï† Ïˆ : M âŸ¶ N) : (Ï† + Ïˆ).app U = Ï†.app U + Ïˆ.app U := rfl
@[simp] lemma Hom.sub_app (Ï† Ïˆ : M âŸ¶ N) : (Ï† - Ïˆ).app U = Ï†.app U - Ïˆ.app U := rfl
@[simp] lemma Hom.zero_app : (0 : M âŸ¶ N).app U = 0 := rfl
@[simp] lemma Hom.id_app (M : X.Modules) : (ğŸ™ M :).app U = ğŸ™ _ := rfl
@[simp] lemma Hom.comp_app (Ï† : M âŸ¶ N) (Ïˆ : N âŸ¶ K) : (Ï† â‰« Ïˆ).app U = Ï†.app U â‰« Ïˆ.app U := rfl

@[ext]
lemma hom_ext (f g : M âŸ¶ N) (H : âˆ€ U, f.app U = g.app U) : f = g := by
  apply SheafOfModules.hom_ext
  ext U x
  exact congr($(H U.unop) x)

lemma isSheaf (M : X.Modules) : M.presheaf.IsSheaf := SheafOfModules.isSheaf M

@[simp] lemma toPresheaf_obj : (toPresheaf X).obj M = M.presheaf := rfl
@[simp] lemma toPresheaf_map : (toPresheaf X).map Ï† = Ï†.mapPresheaf := rfl

lemma Hom.isIso_iff_isIso_app {M N : X.Modules} {Ï† : M âŸ¶ N} :
    IsIso Ï† â†” âˆ€ U, IsIso (Ï†.app U) := by
  rw [â† isIso_iff_of_reflects_iso _ (toPresheaf X), NatTrans.isIso_iff_isIso_app]
  simp [Opposite.op_surjective.forall]

instance [IsIso Ï†] : IsIso (Ï†.app U) := Hom.isIso_iff_isIso_app.mp â€¹_â€º _

@[simp, push â†]
lemma inv_app [IsIso Ï†] : (inv Ï†).app U = inv (Ï†.app U) := by
  apply IsIso.eq_inv_of_hom_inv_id
  simp [â† Hom.comp_app]

end Presheaf

noncomputable section Functorial

variable (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (h : Z âŸ¶ T)

/-- The pushforward functor for categories of sheaves of modules over schemes. -/
def pushforward : X.Modules â¥¤ Y.Modules :=
  SheafOfModules.pushforward f.toRingCatSheafHom

@[simp]
lemma pushforward_obj_obj (M : X.Modules) (U : Y.Opens) :
    Î“((pushforward f).obj M, U) = Î“(M, f â»Â¹áµ U) := rfl

@[simp]
lemma pushforward_obj_presheaf_map {U V : Y.Opens} (i : U âŸ¶ V) :
    ((pushforward f).obj M).presheaf.map i.op = M.presheaf.map ((Opens.map f.base).map i).op := rfl

@[simp]
lemma pushforward_map_app (Ï† : M âŸ¶ N) (U : Y.Opens) :
    ((pushforward f).map Ï†).app U = Ï†.app (f â»Â¹áµ U) := rfl

/-- The pullback functor for categories of sheaves of modules over schemes. -/
def pullback : Y.Modules â¥¤ X.Modules :=
  SheafOfModules.pullback f.toRingCatSheafHom

/-- The pullback functor for categories of sheaves of modules over schemes
is left adjoint to the pushforward functor. -/
def pullbackPushforwardAdjunction : pullback f âŠ£ pushforward f :=
  SheafOfModules.pullbackPushforwardAdjunction _

section

attribute [local instance] preservesBinaryBiproducts_of_preservesBinaryCoproducts
  preservesBinaryBiproducts_of_preservesBinaryProducts

instance : (pullback f).IsLeftAdjoint := (pullbackPushforwardAdjunction f).isLeftAdjoint
instance : (pushforward f).IsRightAdjoint := (pullbackPushforwardAdjunction f).isRightAdjoint
instance : (pushforward f).Additive := Functor.additive_of_preservesBinaryBiproducts _
instance : (pullback f).Additive := Functor.additive_of_preservesBinaryBiproducts _

end

variable (X) in
/-- The pushforward of sheaves of modules by the identity morphism identifies
to the identity functor. -/
def pushforwardId : pushforward (ğŸ™ X) â‰… ğŸ­ _ :=
  SheafOfModules.pushforwardId _

@[simp] lemma pushforwardId_hom_app_app : ((pushforwardId X).hom.app M).app U = ğŸ™ _ := rfl
@[simp] lemma pushforwardId_inv_app_app : ((pushforwardId X).inv.app M).app U = ğŸ™ _ := rfl

variable (X) in
/-- The pullback of sheaves of modules by the identity morphism identifies
to the identity functor. -/
def pullbackId : pullback (ğŸ™ X) â‰… ğŸ­ _ :=
  SheafOfModules.pullbackId _

variable (X) in
lemma conjugateEquiv_pullbackId_hom :
    conjugateEquiv .id (pullbackPushforwardAdjunction (ğŸ™ X)) (pullbackId X).hom =
      (pushforwardId X).inv :=
  SheafOfModules.conjugateEquiv_pullbackId_hom _

/-- The composition of two pushforward functors for sheaves of modules on schemes
identify to the pushforward for the composition. -/
def pushforwardComp :
    pushforward f â‹™ pushforward g â‰… pushforward (f â‰« g) :=
  SheafOfModules.pushforwardComp _ _

@[simp] lemma pushforwardComp_hom_app_app (U) : ((pushforwardComp f g).hom.app M).app U = ğŸ™ _ := rfl
@[simp] lemma pushforwardComp_inv_app_app (U) : ((pushforwardComp f g).inv.app M).app U = ğŸ™ _ := rfl

/-- The composition of two pullback functors for sheaves of modules on schemes
identify to the pullback for the composition. -/
def pullbackComp :
    pullback g â‹™ pullback f â‰… pullback (f â‰« g) :=
  SheafOfModules.pullbackComp _ _

/-- Pushforwards along equal morphisms are isomorphic. -/
def pushforwardCongr {f g : X âŸ¶ Y} (hf : f = g) : pushforward f â‰… pushforward g :=
    pushforwardNatIso _ (Opens.mapIso _ _ (hf â–¸ rfl)) â‰ªâ‰«
      SheafOfModules.pushforwardCongr (by cat_disch)

@[simp] lemma pushforwardCongr_hom_app_app {f g : X âŸ¶ Y} (hf : f = g) (U : Y.Opens) :
    ((pushforwardCongr hf).hom.app M).app U = M.presheaf.map (eqToHom (hf â–¸ rfl)).op := rfl

@[simp] lemma pushforwardCongr_inv_app_app {f g : X âŸ¶ Y} (hf : f = g) (U : Y.Opens) :
    ((pushforwardCongr hf).inv.app M).app U = M.presheaf.map (eqToHom (hf â–¸ rfl)).op := rfl

/-- Inverse images along equal morphisms are isomorphic. -/
def pullbackCongr {f g : X âŸ¶ Y} (hf : f = g) : pullback f â‰… pullback g :=
  eqToIso (hf â–¸ rfl)

lemma conjugateEquiv_pullbackComp_inv :
    conjugateEquiv ((pullbackPushforwardAdjunction g).comp (pullbackPushforwardAdjunction f))
      (pullbackPushforwardAdjunction (f â‰« g)) (pullbackComp f g).inv =
    (pushforwardComp f g).hom :=
  SheafOfModules.conjugateEquiv_pullbackComp_inv _ _

@[reassoc]
lemma pseudofunctor_associativity :
    (pullbackComp f (g â‰« h)).inv â‰«
      Functor.whiskerRight (pullbackComp g h).inv _ â‰« (Functor.associator _ _ _).hom â‰«
        Functor.whiskerLeft _ (pullbackComp f g).hom â‰« (pullbackComp (f â‰« g) h).hom =
    eqToHom (by simp) := by
  let eâ‚ := pullbackComp f (g â‰« h)
  let eâ‚‚ := Functor.isoWhiskerRight (pullbackComp g h) (pullback f)
  let eâ‚ƒ := Functor.isoWhiskerLeft (pullback h) (pullbackComp f g)
  let eâ‚„ := pullbackComp (f â‰« g) h
  change eâ‚.inv â‰« eâ‚‚.inv â‰« (Functor.associator _ _ _).hom â‰« eâ‚ƒ.hom â‰« eâ‚„.hom = _
  have : eâ‚ƒ.hom â‰« eâ‚„.hom = (Functor.associator _ _ _).inv â‰« eâ‚‚.hom â‰« eâ‚.hom :=
    congr_arg Iso.hom (SheafOfModules.pullback_assoc.{u}
      h.toRingCatSheafHom g.toRingCatSheafHom f.toRingCatSheafHom)
  simp [this]

@[reassoc]
lemma pseudofunctor_left_unitality :
    (pullbackComp f (ğŸ™ Y)).inv â‰«
      Functor.whiskerRight (pullbackId Y).hom (pullback f) â‰« (Functor.leftUnitor _).hom =
        eqToHom (by simp) := by
  let eâ‚ := pullbackComp f (ğŸ™ _)
  let eâ‚‚ := Functor.isoWhiskerRight (pullbackId Y) (pullback f)
  let eâ‚ƒ := (pullback f).leftUnitor
  change eâ‚.inv â‰« eâ‚‚.hom â‰« eâ‚ƒ.hom = _
  have : eâ‚.hom = eâ‚‚.hom â‰« eâ‚ƒ.hom :=
    congr_arg Iso.hom (SheafOfModules.pullback_id_comp.{u} f.toRingCatSheafHom)
  simp [â† this]

@[reassoc]
lemma pseudofunctor_right_unitality :
    (pullbackComp (ğŸ™ X) f).inv â‰«
      Functor.whiskerLeft (pullback f) (pullbackId X).hom â‰« (Functor.rightUnitor _).hom =
        eqToHom (by simp) := by
  let eâ‚ := pullbackComp (ğŸ™ _) f
  let eâ‚‚ := Functor.isoWhiskerLeft (pullback f) (pullbackId _)
  let eâ‚ƒ := (pullback f).rightUnitor
  change eâ‚.inv â‰« eâ‚‚.hom â‰« eâ‚ƒ.hom = _
  have : eâ‚.hom = eâ‚‚.hom â‰« eâ‚ƒ.hom :=
    congr_arg Iso.hom (SheafOfModules.pullback_comp_id.{u} f.toRingCatSheafHom)
  simp [â† this]

attribute [local simp] pseudofunctor_associativity pseudofunctor_left_unitality
  pseudofunctor_right_unitality Bicategory.toNatTrans_conjugateEquiv
  conjugateEquiv_pullbackId_hom Adjunction.ofCat_comp conjugateEquiv_pullbackComp_inv in
/-- The pseudofunctor from `Schemeáµ’áµ–` to the bicategory of adjunctions which sends
a scheme `X` to the category `X.Modules` of sheaves of modules over `X`.
(This contains both the covariant and the contravariant functorialities of
these categories.) -/
@[simps! obj_obj map_l map_r map_adj
  mapId_hom_Ï„l mapId_hom_Ï„r mapId_inv_Ï„l mapId_inv_Ï„r
  mapComp_hom_Ï„l mapComp_hom_Ï„r mapComp_inv_Ï„l mapComp_inv_Ï„r]
def pseudofunctor :
    Pseudofunctor (LocallyDiscrete Scheme.{u}áµ’áµ–) (Adj Cat) :=
  LocallyDiscrete.mkPseudofunctor
    (fun X â†¦ Adj.mk (Cat.of X.unop.Modules))
    (fun f â†¦ .mk (pullbackPushforwardAdjunction f.unop).toCat)
    (fun _ â†¦ Adj.isoâ‚‚Mk (Cat.Hom.isoMk (pullbackId _))
        (Cat.Hom.isoMk (pushforwardId _).symm))
    (fun _ _ â†¦ Adj.isoâ‚‚Mk (Cat.Hom.isoMk (pullbackComp _ _).symm)
        (Cat.Hom.isoMk (pushforwardComp _ _)))

end Functorial

noncomputable section Restriction

variable [IsOpenImmersion f]

/-- Restriction of an `ğ’ªâ‚“`-module along an open immersion.
This is isomorphic to the pullback functor (see `restrictFunctorIsoPullback`)
but has better defeqs. -/
def restrictFunctor :
    Y.Modules â¥¤ X.Modules :=
  letI Î± : X.presheaf âŸ¶ f.opensFunctor.op â‹™ Y.presheaf := { app U := (f.appIso U.unop).inv }
  SheafOfModules.pushforward (F := f.opensFunctor)
    âŸ¨Functor.whiskerRight Î± (forgetâ‚‚ CommRingCat RingCat)âŸ©

/-- The restriction of a module along an open immersion. -/
abbrev restrict (M : Y.Modules) (f : X âŸ¶ Y) [IsOpenImmersion f] : X.Modules :=
  (restrictFunctor f).obj M

@[simp] lemma restrict_obj (M : Y.Modules) (f : X âŸ¶ Y) [IsOpenImmersion f] (U) :
    Î“(M.restrict f, U) = Î“(M, f ''áµ U) := rfl

@[simp] lemma restrict_map (M : Y.Modules) (f : X âŸ¶ Y) [IsOpenImmersion f] {U V} (i : U âŸ¶ V) :
    (M.restrict f).presheaf.map i.op = M.presheaf.map (f.opensFunctor.map i).op := rfl

/-- The restriction of a module along an open immersion. -/
def restrictFunctorAdjCounitIso : pushforward f â‹™ restrictFunctor f â‰… ğŸ­ _ :=
  letI := CategoryTheory.Functor.isContinuous_comp.{u} f.opensFunctor (Opens.map f.base)
    (Opens.grothendieckTopology X) (Opens.grothendieckTopology Y) (Opens.grothendieckTopology X)
  (SheafOfModules.pushforwardComp _ _) â‰ªâ‰« pushforwardNatIso _ (NatIso.ofComponents
      (fun U â†¦ eqToIso (f.preimage_image_eq U).symm) fun _ â†¦ rfl) â‰ªâ‰«
    SheafOfModules.pushforwardCongr (by ext U x; exact
      congr($(f.appIso_inv_app_presheafMap U.unop) x)) â‰ªâ‰« SheafOfModules.pushforwardId _

/-- Restriction is right adjoint to pushforward. -/
def restrictAdjunction : restrictFunctor f âŠ£ pushforward f := by
  refine pushforwardPushforwardAdj (by exact f.isOpenEmbedding.isOpenMap.adjunction) _ _ ?_ ?_
  Â· ext U x; exact congr($((f.app_appIso_inv _).symm).hom x)
  Â· ext U x
    have : (f.appIso U.unop).inv â‰« f.app _ â‰«
      X.presheaf.map (eqToHom (f.preimage_image_eq U.unop).symm).op = ğŸ™ _ := by
      rw [Scheme.Hom.appIso_inv_app_assoc, â† Functor.map_comp, â† X.presheaf.map_id]; rfl
    exact congr($this x)

instance : IsIso (restrictAdjunction f).counit :=
  inferInstanceAs (IsIso <| (restrictFunctorAdjCounitIso f).hom)

instance : (restrictFunctor f).IsLeftAdjoint := (restrictAdjunction f).isLeftAdjoint
instance : (pushforward f).Full := (restrictAdjunction f).fullyFaithfulROfIsIsoCounit.full
instance : (pushforward f).Faithful := (restrictAdjunction f).fullyFaithfulROfIsIsoCounit.faithful

@[simp]
lemma restrictAdjunction_unit_app_app (M : Y.Modules) (U : Y.Opens) :
    ((restrictAdjunction f).unit.app M).app U =
      M.presheaf.map (homOfLE (f.image_preimage_le U)).op := rfl

@[simp]
lemma restrictAdjunction_counit_app_app (M : X.Modules) (U : X.Opens) :
    ((restrictAdjunction f).counit.app M).app U =
      M.presheaf.map (eqToHom (f.preimage_image_eq U).symm).op := rfl

/-- Restriction is naturally isomorphic to the inverse image. -/
def restrictFunctorIsoPullback : restrictFunctor f â‰… pullback f :=
  (restrictAdjunction f).leftAdjointUniq (pullbackPushforwardAdjunction f)

/-- Restriction along the identity is isomorphic to the identity. -/
def restrictFunctorId : restrictFunctor (ğŸ™ X) â‰… ğŸ­ _ :=
  SheafOfModules.pushforwardNatIso _ (NatIso.ofComponents (fun _ â†¦ eqToIso (by simp))) â‰ªâ‰«
    SheafOfModules.pushforwardCongr
      (by ext : 3; simp [â† Functor.map_comp, SheafedSpace.sheaf]) â‰ªâ‰«
    SheafOfModules.pushforwardId _

@[simp]
lemma restrictFunctorId_hom_app_app :
    (restrictFunctorId.hom.app M).app U =
      M.presheaf.map (eqToHom (show U = ğŸ™ X ''áµ U by simp)).op := rfl

@[simp]
lemma restrictFunctorId_inv_app_app :
    (restrictFunctorId.inv.app M).app U =
      M.presheaf.map (eqToHom (show ğŸ™ X ''áµ U = U by simp)).op := rfl

/-- Restriction along the composition is isomorphic to the composition of restrictions. -/
def restrictFunctorComp [IsOpenImmersion f] [IsOpenImmersion g] :
    restrictFunctor (f â‰« g) â‰… restrictFunctor g â‹™ restrictFunctor f :=
  have : (f.opensFunctor â‹™ g.opensFunctor).IsContinuous
      (Opens.grothendieckTopology X) (Opens.grothendieckTopology Z) :=
    Functor.isContinuous_comp _ _ _ (Opens.grothendieckTopology _) _
  SheafOfModules.pushforwardNatIso _ (NatIso.ofComponents fun _ â†¦ eqToIso (by simp)) â‰ªâ‰«
    SheafOfModules.pushforwardCongr (by ext : 3; simp [â† Functor.map_comp, SheafedSpace.sheaf]) â‰ªâ‰«
    (SheafOfModules.pushforwardComp _ _).symm

@[simp]
lemma restrictFunctorComp_hom_app_app [IsOpenImmersion g] (M : Z.Modules) :
    ((restrictFunctorComp f g).hom.app M).app U = M.presheaf.map (eqToHom (by simp)).op := rfl

@[simp]
lemma restrictFunctorComp_inv_app_app [IsOpenImmersion g] (M : Z.Modules) :
    ((restrictFunctorComp f g).inv.app M).app U = M.presheaf.map (eqToHom (by simp)).op := rfl

/-- Restriction along equal morphisms are isomorphic. -/
def restrictFunctorCongr {f g : X âŸ¶ Y} (hf : f = g) [IsOpenImmersion f] [IsOpenImmersion g] :
    restrictFunctor f â‰… restrictFunctor g :=
  SheafOfModules.pushforwardNatIso _ (NatIso.ofComponents fun _ â†¦ eqToIso (by simp [hf])) â‰ªâ‰«
    SheafOfModules.pushforwardCongr (by ext : 3; subst hf; simp)

@[simp]
lemma restrictFunctorCongr_hom_app_app {f g : X âŸ¶ Y} (hf : f = g) [IsOpenImmersion f]
    [IsOpenImmersion g] (M : Y.Modules) :
    ((restrictFunctorCongr hf).hom.app M).app U = M.presheaf.map (eqToHom (by simp [hf])).op := rfl

@[simp]
lemma restrictFunctorCongr_inv_app_app {f g : X âŸ¶ Y} (hf : f = g) [IsOpenImmersion f]
    [IsOpenImmersion g] (M : Y.Modules) :
    ((restrictFunctorCongr hf).inv.app M).app U = M.presheaf.map (eqToHom (by simp [hf])).op := rfl

/-- Restriction along open immersions commutes with taking stalks. -/
def restrictStalkNatIso (f : X âŸ¶ Y) [IsOpenImmersion f] (x : X) :
    restrictFunctor f â‹™ toPresheaf _ â‹™ TopCat.Presheaf.stalkFunctor _ x â‰…
    toPresheaf _ â‹™ TopCat.Presheaf.stalkFunctor _ (f x) :=
  haveI := Functor.initial_of_adjunction (f.isOpenEmbedding.isOpenMap.adjunctionNhds x)
  (toPresheaf _ â‹™ (Functor.whiskeringLeft (OpenNhds (f x))áµ’áµ– Y.Opensáµ’áµ– Ab).obj
      (OpenNhds.inclusion (f x)).op).isoWhiskerLeft
      (Functor.Final.colimIso (f.isOpenEmbedding.isOpenMap.functorNhds x).op)

@[simp]
lemma germ_restrictStalkNatIso_hom_app (f : X âŸ¶ Y) [IsOpenImmersion f]
    (x : X) (M : Y.Modules) (hxU : x âˆˆ U) :
    ((restrictFunctor f).obj M).presheaf.germ U _ hxU â‰«
      (restrictStalkNatIso f x).hom.app M = M.presheaf.germ _ _ (by simpa) :=
  haveI := Functor.initial_of_adjunction (f.isOpenEmbedding.isOpenMap.adjunctionNhds x)
  Functor.Final.Î¹_colimitIso_hom
    (f.isOpenEmbedding.isOpenMap.functorNhds x).op
    ((OpenNhds.inclusion ((ConcreteCategory.hom f.base) x)).op â‹™ M.presheaf) _

@[simp]
lemma germ_restrictStalkNatIso_inv_app (f : X âŸ¶ Y) [IsOpenImmersion f]
    (x : X) (M : Y.Modules) (hxU : x âˆˆ U) :
    M.presheaf.germ _ _ (by simpa) â‰« (restrictStalkNatIso f x).inv.app M =
      ((restrictFunctor f).obj M).presheaf.germ U _ hxU := by
  rw [â† germ_restrictStalkNatIso_hom_app f x M hxU, Category.assoc, â† NatTrans.comp_app,
    Iso.hom_inv_id]
  simp

end Restriction

end AlgebraicGeometry.Scheme.Modules
