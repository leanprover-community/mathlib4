/-
Copyright (c) 2024 Weihong Xu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Johan Commelin, Amelia Livingston, Sophie Morel, Jujian Zhang, Weihong Xu
-/

import Mathlib.Algebra.Module.LocalizedModule
import Mathlib.AlgebraicGeometry.StructureSheaf
import Mathlib.AlgebraicGeometry.Modules.Sheaf
import Mathlib.Algebra.Category.ModuleCat.Sheaf
import Mathlib.Algebra.Category.ModuleCat.FilteredColimits

/-!

# Construction of M^~

Given any commutative ring `R` and `R`-module `M`, we construct the sheaf `M^~` of `ğ’ª_SpecR`-modules
such that `M^~(U)` is the set of dependent functions that are locally fractions.

## Main definitions

* `ModuleCat.tildeInAddCommGrp` : `M^~` as a sheaf of abelian groups.
* `ModuleCat.tilde` : `M^~` as a sheaf of `ğ’ª_{Spec R}`-modules.
* `ModuleCat.tilde.stalkIso`: The isomorphism of `R`-modules from the stalk of `M^~` at `x` to
the localization of `M` at the prime ideal corresponding to `x`.

## Technical note

To get the `R`-module structure on the stalks on `M^~`, we had to define
`ModuleCat.tildeInModuleCat`, which is `M^~` seen as sheaf of `R`-modules. We get it by
applying a forgetful functor to `ModuleCat.tilde M`.

-/

universe u

open TopCat AlgebraicGeometry TopologicalSpace CategoryTheory Opposite

variable {R : Type u} [CommRing R] (M : ModuleCat.{u} R)

namespace ModuleCat

namespace Tilde

/-- For an `R`-module `M` and a point `P` in `Spec R`, `Localizations P` is the localized module
`M` at the prime ideal `P`. -/
abbrev Localizations (P : PrimeSpectrum.Top R) :=
LocalizedModule P.asIdeal.primeCompl M

/-- For any open subset `U âŠ† Spec R`, `IsFraction` is the predicate expressing that a function
`f : âˆ_{x âˆˆ U}, Mâ‚“` is such that for any `ğ”­ âˆˆ U`, `f ğ”­ = m / s` for some `m : M` and `s âˆ‰ ğ”­`.
In short `f` is a fraction on `U`. -/
def isFraction {U : Opens (PrimeSpectrum R)} (f : âˆ€ ğ”­ : U, Localizations M ğ”­.1) : Prop :=
  âˆƒ (m : M) (s : R),
    âˆ€ x : U, Â¬s âˆˆ x.1.asIdeal âˆ§ s â€¢ f x = LocalizedModule.mkLinearMap x.1.asIdeal.primeCompl M m

/--
The property of a function `f : âˆ_{x âˆˆ U}, Mâ‚“` being a fraction is stable under restriction.
-/
def isFractionPrelocal : PrelocalPredicate (Localizations M) where
  pred {U} f := isFraction M f
  res := by rintro V U i f âŸ¨m, s, wâŸ©; exact âŸ¨m, s, fun x => w (i x)âŸ©

/--
For any open subset `U âŠ† Spec R`, `IsLocallyFraction` is the predicate expressing that a function
`f : âˆ_{x âˆˆ U}, Mâ‚“` is such that for any `ğ”­ âˆˆ U`, there exists an open neighbourhood `V âˆ‹ ğ”­`, such
that for any `ğ”® âˆˆ V`, `f ğ”® = m / s` for some `m : M` and `s âˆ‰ ğ”®`.
In short `f` is locally a fraction on `U`.
-/
def isLocallyFraction : LocalPredicate (Localizations M) := (isFractionPrelocal M).sheafify

@[simp]
theorem isLocallyFraction_pred {U : Opens (PrimeSpectrum.Top R)}
    (f : âˆ€ x : U, Localizations M x) :
    (isLocallyFraction M).pred f =
      âˆ€ y : U,
        âˆƒ (V : _) (_ : y.1 âˆˆ V) (i : V âŸ¶ U),
          âˆƒ (m : M) (s: R), âˆ€ x : V, Â¬s âˆˆ x.1.asIdeal âˆ§ s â€¢ f (i x) =
            LocalizedModule.mkLinearMap x.1.asIdeal.primeCompl M m :=
  rfl

/- M_x is an O_SpecR(U)-module when x is in U -/
noncomputable instance (U : (Opens (PrimeSpectrum.Top R))áµ’áµ–) (x : U.unop):
    Module ((Spec.structureSheaf R).val.obj U) (Localizations M x):=
  Module.compHom (R := (Localization.AtPrime x.1.asIdeal)) _
    (StructureSheaf.openToLocalization R U.unop x x.2 :
      (Spec.structureSheaf R).val.obj U â†’+* Localization.AtPrime x.1.asIdeal)

@[simp]
lemma sections_smul_localizations_def
    {U : (Opens (PrimeSpectrum.Top R))áµ’áµ–} (x : U.unop)
    (r : (Spec.structureSheaf R).val.obj U)
    (m : Localizations M â†‘x) :
  r â€¢ m = r.1 x â€¢ m := rfl

/--
For any `R`-module `M` and any open subset `U âŠ† Spec R`, `M^~(U)` is an `ğ’ª_{Spec R}(U)`-submodule
of `âˆ_{ğ”­ âˆˆ U} M_ğ”­`. -/
def sectionsSubmodule (U : (Opens (PrimeSpectrum R))áµ’áµ–) :
    Submodule ((Spec.structureSheaf R).1.obj U) (âˆ€ x : U.unop, Localizations M x.1) where
  carrier := { f | (isLocallyFraction M).pred f }
  zero_mem' x := âŸ¨unop U, x.2, ğŸ™ _, 0, 1, fun y =>
    âŸ¨Ideal.ne_top_iff_one _ |>.1 y.1.isPrime.1, by simpâŸ©âŸ©
  add_mem' := by
    intro a b ha hb x
    rcases ha x with âŸ¨Va, ma, ia, ra, sa, waâŸ©
    rcases hb x with âŸ¨Vb, mb, ib, rb, sb, wbâŸ©
    refine âŸ¨Va âŠ“ Vb, âŸ¨ma, mbâŸ©, Opens.infLELeft _ _ â‰« ia,  sbâ€¢ ra+ saâ€¢rb , sa * sb, ?_âŸ©
    intro y
    rcases wa (Opens.infLELeft _ _ y : Va) with âŸ¨nma, waâŸ©
    rcases wb (Opens.infLERight _ _ y : Vb) with âŸ¨nmb, wbâŸ©
    fconstructor
    Â· intro H; cases y.1.isPrime.mem_or_mem H <;> contradiction
    Â· simp only [Opens.coe_inf, Pi.add_apply, smul_add, map_add,
        LinearMapClass.map_smul] at wa wb âŠ¢
      rw [â† wa, â† wb, â† mul_smul, â† mul_smul]
      congr 2
      simp [mul_comm]
  smul_mem' := by
    intro r a ha x
    rcases ha x with âŸ¨Va, ma, ia, ra, sa, waâŸ©
    rcases r.2 x with âŸ¨Vr, mr, ir, rr, sr, wrâŸ©
    refine âŸ¨Va âŠ“ Vr, âŸ¨ma, mrâŸ©, Opens.infLELeft _ _ â‰« ia, rrâ€¢ra, sr*sa, ?_âŸ©
    intro y
    rcases wa (Opens.infLELeft _ _ y : Va) with âŸ¨nma, waâŸ©
    rcases wr (Opens.infLERight _ _ y) with âŸ¨nmr, wrâŸ©
    fconstructor
    Â· intro H; cases y.1.isPrime.mem_or_mem H <;> contradiction
    Â· simp only [Opens.coe_inf, Pi.smul_apply, LinearMapClass.map_smul] at wa wr âŠ¢
      rw [mul_comm, â† Algebra.smul_def] at wr
      rw [sections_smul_localizations_def, â† wa, â† mul_smul, â† smul_assoc, mul_comm sr, mul_smul,
        wr, mul_comm rr, Algebra.smul_def, â† map_mul]
      rfl

end Tilde

/--
For any `R`-module `M`, `TildeInType R M` is the sheaf of set on `Spec R` whose sections on `U` are
the dependent functions that are locally fractions. This is often denoted by `M^~`.

See also `Tilde.isLocallyFraction`.
-/
def tildeInType : Sheaf (Type u) (PrimeSpectrum.Top R) :=
  subsheafToTypes (Tilde.isLocallyFraction M)

instance (U : (Opens (PrimeSpectrum.Top R))áµ’áµ–) :
    AddCommGroup (M.tildeInType.1.obj U) :=
  inferInstanceAs $ AddCommGroup (Tilde.sectionsSubmodule M U)

/--
`M^~` as a presheaf of abelian groups over `Spec R`
-/
def pretildeInAddCommGrp : Presheaf AddCommGrp (PrimeSpectrum.Top R) where
  obj U := .of ((M.tildeInType).1.obj U)
  map {U V} i :=
    { toFun := M.tildeInType.1.map i
      map_zero' := rfl
      map_add' := fun x y => rfl}

/--
`M^~` as a sheaf of abelian groups over `Spec R`
-/
def tildeInAddCommGrp : Sheaf AddCommGrp (PrimeSpectrum.Top R) :=
  âŸ¨M.pretildeInAddCommGrp,
    TopCat.Presheaf.isSheaf_iff_isSheaf_comp (forget AddCommGrp) _ |>.mpr
      (TopCat.Presheaf.isSheaf_of_iso (NatIso.ofComponents (fun _ => Iso.refl _) fun _ => rfl)
        M.tildeInType.2)âŸ©

noncomputable instance (U : (Opens (PrimeSpectrum.Top R))áµ’áµ–) :
    Module ((Spec (.of R)).ringCatSheaf.1.obj U) (M.tildeInAddCommGrp.1.obj U) :=
  inferInstanceAs $ Module _ (Tilde.sectionsSubmodule M U)

/--
`M^~` as a sheaf of `ğ’ª_{Spec R}`-modules
-/
noncomputable def tilde : (Spec (CommRingCat.of R)).Modules where
  val :=
  { presheaf := M.tildeInAddCommGrp.1
    module := inferInstance
    map_smul := fun _ _ _ => rfl }
  isSheaf := M.tildeInAddCommGrp.2

/--
This is `M^~` as a sheaf of `R`-modules.
-/
noncomputable def tildeInModuleCat :
    TopCat.Presheaf (ModuleCat R) (PrimeSpectrum.Top R) :=
  (PresheafOfModules.forgetToPresheafModuleCat (op âŠ¤) $
    Limits.initialOpOfTerminal Limits.isTerminalTop).obj (tilde M).1 â‹™
  ModuleCat.restrictScalars (StructureSheaf.globalSectionsIso R).hom

namespace Tilde

@[simp]
theorem res_apply (U V : Opens (PrimeSpectrum.Top R)) (i : V âŸ¶ U)
    (s : (tildeInModuleCat M).obj (op U)) (x : V) :
    ((tildeInModuleCat M).map i.op s).1 x = (s.1 (i x) : _) :=
  rfl

lemma smul_section_apply (r : R) (U : Opens (PrimeSpectrum.Top R))
    (s : (tildeInModuleCat M).1.obj (op U)) (x : U) :
    (r â€¢ s).1 x = r â€¢ (s.1 x) := rfl

lemma smul_germ (r : R) (U : Opens (PrimeSpectrum.Top R)) (x : U)
    (s : (tildeInModuleCat M).1.obj (op U)) :
    r â€¢ (tildeInModuleCat M).germ x s =
    (tildeInModuleCat M).germ x (r â€¢ s) := by rw [map_smul]

/-- The ring homomorphism that takes a section of the structure sheaf of `R` on the open set `U`,
implemented as a subtype of dependent functions to localizations at prime ideals, and evaluates
the section on the point corresponding to a given prime ideal. -/
def openToLocalization (U : Opens (PrimeSpectrum.Top R)) (x : PrimeSpectrum.Top R) (hx : x âˆˆ U) :
    (tildeInModuleCat M).1.obj (op U) âŸ¶
    ModuleCat.of R (LocalizedModule x.asIdeal.primeCompl M) where
  toFun s := (s.1 âŸ¨x, hxâŸ© : _)
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

@[simp]
theorem coe_openToLocalization (U : Opens (PrimeSpectrum.Top R)) (x : PrimeSpectrum.Top R)
    (hx : x âˆˆ U) :
    (openToLocalization M U x hx :
        (tildeInAddCommGrp M).1.obj (op U) â†’ LocalizedModule x.asIdeal.primeCompl M) =
      fun s => (s.1 âŸ¨x, hxâŸ© : _) :=
  rfl

theorem openToLocalization_apply (U : Opens (PrimeSpectrum.Top R)) (x : PrimeSpectrum.Top R)
    (hx : x âˆˆ U) (s : (tildeInAddCommGrp M).1.obj (op U)) :
    openToLocalization M U x hx s = (s.1 âŸ¨x, hxâŸ© : _) :=
  rfl

/--
The morphism of `R`-modules from the stalk of `M^~` at `x` to the localization of `M` at the
prime ideal of `R` corresponding to `x`.
-/
noncomputable def stalkToFiberLinearMap (x : PrimeSpectrum.Top R) :
    TopCat.Presheaf.stalk (tildeInModuleCat M) x âŸ¶
    ModuleCat.of R (LocalizedModule x.asIdeal.primeCompl M) :=
  Limits.colimit.desc ((OpenNhds.inclusion x).op â‹™ (tildeInModuleCat M))
    { pt := _
      Î¹ :=
      { app := fun U =>
          (openToLocalization M ((OpenNhds.inclusion _).obj (unop U)) x (unop U).2)
        naturality := fun {U V} i => by aesop_cat } }

@[simp]
theorem germ_comp_stalkToFiberLinearMap (U : Opens (PrimeSpectrum.Top R)) (x : U) :
    TopCat.Presheaf.germ (tildeInModuleCat M) x â‰« stalkToFiberLinearMap M x =
    openToLocalization M U x x.2 :=
  Limits.colimit.Î¹_desc _ _

@[simp]
theorem stalkToFiberLinearMap_germ' (U : Opens (PrimeSpectrum.Top R)) (x : PrimeSpectrum.Top R)
    (hx : x âˆˆ U) (s : (tildeInModuleCat M).1.obj (op U)) :
    stalkToFiberLinearMap M x
      (TopCat.Presheaf.germ (tildeInModuleCat M) âŸ¨x, hxâŸ© s) = (s.1 âŸ¨x, hxâŸ© : _) :=
  DFunLike.ext_iff.1 (germ_comp_stalkToFiberLinearMap M U âŸ¨x, hxâŸ© : _) s

@[simp]
theorem stalkToFiberLinearMap_germ (U : Opens (PrimeSpectrum.Top R)) (x : U)
    (s : (tildeInModuleCat M).1.obj (op U)) :
    stalkToFiberLinearMap M x (TopCat.Presheaf.germ (tildeInModuleCat M) x s) =
    s.1 x := by
  cases x; exact stalkToFiberLinearMap_germ' M U _ _ _

/--
If `U` is an open subset of `Spec R`, this is the morphism of `R`-modules from `M` to
`M^~(U)`.
-/
def toOpen (U : Opens (PrimeSpectrum.Top R)) :
    ModuleCat.of R M âŸ¶ (tildeInModuleCat M).1.obj (op U) where
  toFun f :=
  âŸ¨fun x => LocalizedModule.mkLinearMap _ _ f, fun x =>
    âŸ¨U, x.2, ğŸ™ _, f, 1, fun y => âŸ¨(Ideal.ne_top_iff_one _).1 y.1.2.1, by simpâŸ©âŸ©âŸ©
  map_add' f g := Subtype.eq <| funext fun x => LinearMap.map_add _ _ _
  map_smul' r m := by
    simp only [isLocallyFraction_pred, LocalizedModule.mkLinearMap_apply, LinearMapClass.map_smul,
      RingHom.id_apply]
    rfl

@[simp]
theorem toOpen_res (U V : Opens (PrimeSpectrum.Top R)) (i : V âŸ¶ U) :
    toOpen M U â‰« (tildeInModuleCat M).map i.op = toOpen M V :=
  rfl

/--
If `x` is a point of `Spec R`, this is the morphism of `R`-modules from `M` to the stalk of
`M^~` at `x`.
-/
noncomputable def toStalk (x : PrimeSpectrum.Top R) :
    ModuleCat.of R M âŸ¶ TopCat.Presheaf.stalk (tildeInModuleCat M) x :=
  (toOpen M âŠ¤ â‰« TopCat.Presheaf.germ (tildeInModuleCat M) âŸ¨x, by trivialâŸ©)

@[simp]
theorem toOpen_germ (U : Opens (PrimeSpectrum.Top R)) (x : U) :
    toOpen M U â‰« TopCat.Presheaf.germ (tildeInModuleCat M) x = toStalk M x := by
  rw [â† toOpen_res M âŠ¤ U (homOfLE le_top : U âŸ¶ âŠ¤), Category.assoc, Presheaf.germ_res]; rfl

theorem germ_toOpen (U : Opens (PrimeSpectrum.Top R)) (x : U) (f : M) :
    (M.tildeInModuleCat.germ x) ((ModuleCat.Tilde.toOpen M U) f) = toStalk M x f := by
  rw [â† toOpen_germ]; rfl

lemma isUnit_toStalk (x : PrimeSpectrum.Top R) (r : x.asIdeal.primeCompl) :
    IsUnit ((algebraMap R (Module.End R ((tildeInModuleCat M).stalk x))) r) := by
  rw [Module.End_isUnit_iff]
  refine âŸ¨?_, ?_âŸ©
  Â· rw [â† LinearMap.ker_eq_bot, eq_bot_iff]
    rintro st (h : r.1 â€¢ st = 0)
    simp only [LinearMap.mem_ker, Module.algebraMap_end_apply, Submodule.mem_bot] at h âŠ¢

    obtain âŸ¨U, mem, s, rflâŸ© := TopCat.Presheaf.germ_exist (F := (tildeInModuleCat M)) x st
    erw [â† (M.tildeInModuleCat.germ âŸ¨x, memâŸ©).map_smul r.1 s] at h
    obtain âŸ¨âŸ¨âŸ¨W, (mem_W : x âˆˆ W)âŸ©âŸ©, iU, (h : M.tildeInModuleCat.map _ _ = 0)âŸ© :=
      Limits.Concrete.colimit_rep_eq_zero (hx := h)
    dsimp only [Functor.comp_obj, Functor.op_obj, OpenNhds.inclusion_obj, Functor.comp_map,
      Functor.op_map] at h

    obtain âŸ¨W', (mem_W' : x âˆˆ W'), (iW : W' âŸ¶ W), num, _, _âŸ© :=
      ((tildeInModuleCat M).map iU) s |>.2 âŸ¨x, mem_WâŸ©
    let O := W' âŠ“ (PrimeSpectrum.basicOpen r)
    suffices (tildeInModuleCat M).map
        (op $ (homOfLE $ inf_le_left.trans (leOfHom $ iW â‰« iU.unop) : O âŸ¶ U)) s = 0 by
      have := congr((tildeInModuleCat M).germ (âŸ¨x, âŸ¨mem_W', r.2âŸ©âŸ© :
        (W' âŠ“ PrimeSpectrum.basicOpen r.1 : Opens _)) $this)
      rw [this.symm.trans (TopCat.Presheaf.germ_res_apply _ _ _ _) |>.symm, map_zero]

    refine Subtype.ext $ funext fun q => show _ = 0 from ?_
    apply_fun (tildeInModuleCat M).map (op iW) at h
    rw [map_smul] at h
    replace h := congr_fun (Subtype.ext_iff.1 h) âŸ¨q.1, q.2.1âŸ©
    exact LocalizedModule.eq_zero_of_smul_eq_zero (hx := h) q.2.2

  Â· intro st
    obtain âŸ¨U, mem, s, rflâŸ© := TopCat.Presheaf.germ_exist (F := (tildeInModuleCat M)) x st
    let O := U âŠ“ (PrimeSpectrum.basicOpen r)
    have mem_O : x âˆˆ O := âŸ¨mem, r.2âŸ©
    refine âŸ¨TopCat.Presheaf.germ (tildeInModuleCat M) âŸ¨x, mem_OâŸ©
      âŸ¨fun q => (Localization.mk 1 âŸ¨r, q.2.2âŸ© : Localization.AtPrime q.1.asIdeal) â€¢ s.1
        âŸ¨q.1, q.2.1âŸ©, fun q => ?_âŸ©, ?_âŸ©
    Â· obtain âŸ¨V, mem_V, (iV : V âŸ¶ U), num, den, hVâŸ© := s.2 âŸ¨q.1, q.2.1âŸ©
      refine âŸ¨V âŠ“ O, âŸ¨mem_V, q.2âŸ©, homOfLE inf_le_right, num, r * den, fun y => ?_âŸ©
      obtain âŸ¨h1, h2âŸ© := hV âŸ¨y, y.2.1âŸ©
      refine âŸ¨y.1.asIdeal.primeCompl.mul_mem y.2.2.2 h1, ?_âŸ©
      simp only [Opens.coe_inf, isLocallyFraction_pred, LocalizedModule.mkLinearMap_apply] at h2 âŠ¢
      rw [LocalizedModule.smul_eq_iff_of_mem (S := y.1.asIdeal.primeCompl) (hr := h1),
        LocalizedModule.mk_smul_mk, one_smul, mul_one] at h2
      rw [h2, LocalizedModule.mk_smul_mk, one_smul, LocalizedModule.smul'_mk, LocalizedModule.mk_eq]
      refine âŸ¨1, by simp only [one_smul]; rflâŸ©
    Â· simp only [isLocallyFraction_pred, LocalizedModule.mkLinearMap_apply,
        Module.algebraMap_end_apply]
      rw [â† map_smul]
      fapply TopCat.Presheaf.germ_ext
      Â· exact O
      Â· exact mem_O
      Â· exact ğŸ™ _
      Â· exact homOfLE inf_le_left
      refine Subtype.eq <| funext fun y => ?_
      simp only [isLocallyFraction_pred, LocalizedModule.mkLinearMap_apply, op_id,
        CategoryTheory.Functor.map_id, LinearMapClass.map_smul,
        id_apply]
      rw [smul_section_apply, LocalizedModule.smul_eq_iff_of_mem]
      rfl

/--
The morphism of `R`-modules from the localization of `M` at the prime ideal corresponding to `x`
to the stalk of `M^~` at `x`.
-/
noncomputable def localizationToStalk (x : PrimeSpectrum.Top R) :
    ModuleCat.of R (LocalizedModule x.asIdeal.primeCompl M) âŸ¶
    (TopCat.Presheaf.stalk (tildeInModuleCat M) x) :=
  LocalizedModule.lift _ (toStalk M x) $ isUnit_toStalk M x

@[simp]
theorem toStalk_comp_stalkToFiberLinearMap (x : PrimeSpectrum.Top R) :
    toStalk M x â‰« stalkToFiberLinearMap M x =
    LocalizedModule.mkLinearMap x.asIdeal.primeCompl M := by
  erw [toStalk, Category.assoc, germ_comp_stalkToFiberLinearMap]; rfl

theorem stalkToFiberRingHom_toStalk (x : PrimeSpectrum.Top R) (m : M) :
    (stalkToFiberLinearMap M x) ((toStalk M x) m) =
    LocalizedModule.mk m 1 :=
  LinearMap.ext_iff.1 (toStalk_comp_stalkToFiberLinearMap M x) _

/--
If `U` is an open subset of `Spec R`, `m` is an element of `M` and `r` is an element of `R`
that is invertible on `U` (i.e. does not belong to any prime ideal corresponding to a point
in `U`), this is `m / r` seen as a section of `M^~` over `U`.
-/
def const (m : M) (r : R) (U : Opens (PrimeSpectrum.Top R))
    (hu : âˆ€ x âˆˆ U, r âˆˆ (x : PrimeSpectrum.Top R).asIdeal.primeCompl) :
    (tildeInModuleCat M).obj (op U) :=
  âŸ¨fun x => LocalizedModule.mk m âŸ¨r, hu x x.2âŸ©, fun x =>
    âŸ¨U, x.2, ğŸ™ _, m, r, fun y => âŸ¨hu _ y.2, by
      simp only [LocalizedModule.mkLinearMap_apply, LocalizedModule.smul'_mk]
      rw [LocalizedModule.mk_eq]
      exact âŸ¨1, by simpâŸ©âŸ©âŸ©âŸ©

@[simp]
theorem const_apply (m : M) (r : R) (U : Opens (PrimeSpectrum.Top R))
    (hu : âˆ€ x âˆˆ U, r âˆˆ (x : PrimeSpectrum.Top R).asIdeal.primeCompl) (x : U) :
    (const M m r U hu).1 x = LocalizedModule.mk m âŸ¨r, hu x x.2âŸ© :=
  rfl

theorem const_apply' (m : M) (r : R) (U : Opens (PrimeSpectrum.Top R))
    (hu : âˆ€ x âˆˆ U, r âˆˆ (x : PrimeSpectrum.Top R).asIdeal.primeCompl) (x : U)
    (hx : r âˆˆ (x : PrimeSpectrum.Top R).asIdeal.primeCompl) :
    (const M m r U hu).1 x = LocalizedModule.mk m âŸ¨r, hxâŸ© :=
  rfl

theorem exists_const (U) (s : (tildeInModuleCat M).obj (op U)) (x : PrimeSpectrum.Top R)
    (hx : x âˆˆ U) :
    âˆƒ (V : Opens (PrimeSpectrum.Top R)) (_ : x âˆˆ V) (i : V âŸ¶ U) (f : M) (g : R) (hg : _),
      const M f g V hg = (tildeInModuleCat M).map i.op s :=
  let âŸ¨V, hxV, iVU, f, g, hfgâŸ© := s.2 âŸ¨x, hxâŸ©
  âŸ¨V, hxV, iVU, f, g, fun y hyV => (hfg âŸ¨y, hyVâŸ©).1,
    Subtype.eq <| funext fun y => by
    simp only [isLocallyFraction_pred, LocalizedModule.mkLinearMap_apply, const_apply, res_apply]
    obtain âŸ¨h1, (h2 : g â€¢ s.1 âŸ¨y, _âŸ© = LocalizedModule.mk f 1)âŸ© := hfg y
    replace h2 : s.1 (iVU y) = LocalizedModule.mk f âŸ¨g, by exact h1âŸ© := by
      let x := s.1 (iVU y)
      change g â€¢ x = _ at h2
      change x = _
      clear_value x
      induction x using LocalizedModule.induction_on with
      | h a b =>
        rw [LocalizedModule.smul'_mk, LocalizedModule.mk_eq] at h2
        obtain âŸ¨c, hcâŸ© := h2
        refine LocalizedModule.mk_eq.mpr âŸ¨c, by simpa using hcâŸ©
    rw [h2]âŸ©


@[simp]
theorem res_const (f : M) (g : R) (U hu V hv i) :
    (tildeInModuleCat M).map i (const M f g U hu) = const M f g V hv :=
  rfl

theorem res_const' (f : M) (g : R) (V hv) : (tildeInModuleCat M).map (homOfLE hv).op
    (const M f g (PrimeSpectrum.basicOpen g) fun _ => id) =
      const M f g V hv :=
  rfl

@[simp]
theorem localizationToStalk_mk' (x : PrimeSpectrum.Top R) (f : M) (s : x.asIdeal.primeCompl) :
    localizationToStalk M x (LocalizedModule.mk f s) =
      (tildeInModuleCat M).germ (âŸ¨x, s.2âŸ© : PrimeSpectrum.basicOpen (s : R))
        (const M f s (PrimeSpectrum.basicOpen s) fun _ => id) := by
  simp only [localizationToStalk]
  erw [LocalizedModule.lift_mk]
  change (isUnit_toStalk M x s).unit.inv _ = _
  apply_fun (isUnit_toStalk M x s).unit.1 using
    (Module.End_isUnit_iff _ |>.1 (isUnit_toStalk M x s)).injective
  rw [â† LinearMap.mul_apply]
  simp only [IsUnit.unit_spec, Units.inv_eq_val_inv, IsUnit.mul_val_inv, LinearMap.one_apply,
    Module.algebraMap_end_apply]
  delta toStalk
  erw [comp_apply]
  rw [smul_germ]
  fapply TopCat.Presheaf.germ_ext
  Â· exact PrimeSpectrum.basicOpen s
  Â· exact s.2
  Â· exact homOfLE le_top
  Â· exact ğŸ™ _
  simp only [op_id, CategoryTheory.Functor.map_id, LinearMapClass.map_smul, id_apply]
  refine Subtype.eq <| funext fun y => ?_
  change LocalizedModule.mk _ _ = _
  rw [smul_section_apply]
  simp only [Opens.coe_top, Quiver.Hom.unop_op, isLocallyFraction_pred,
    LocalizedModule.mkLinearMap_apply, const_apply]
  erw [LocalizedModule.smul'_mk, LocalizedModule.mk_eq]
  refine âŸ¨1, ?_âŸ©
  simp only [smul_comm, one_smul]
  rfl

/--
The isomorphism of `R`-modules from the stalk of `M^~` at `x` to the localization of `M` at the
prime ideal corresponding to `x`.
-/
@[simps]
noncomputable def stalkIso (x : PrimeSpectrum.Top R) :
    TopCat.Presheaf.stalk (tildeInModuleCat M) x â‰…
    ModuleCat.of R (LocalizedModule x.asIdeal.primeCompl M) where
  hom := stalkToFiberLinearMap M x
  inv := localizationToStalk M x
  hom_inv_id := by
    fapply TopCat.Presheaf.stalk_hom_ext
    intro U hxU
    ext s
    simp only [Category.comp_id]
    erw [comp_apply, comp_apply, stalkToFiberLinearMap_germ']
    obtain âŸ¨V, hxV, iVU, f, g, (hg : V â‰¤ PrimeSpectrum.basicOpen _), hsâŸ© :=
      exists_const _ _ s x hxU
    erw [â† res_apply M U V iVU s âŸ¨x, hxVâŸ©, â† hs, const_apply, localizationToStalk_mk']
    refine (tildeInModuleCat M).germ_ext V hxV (homOfLE hg) iVU ?_
    dsimp
    erw [â† hs, res_const']
  inv_hom_id := by
    ext x
    induction x using LocalizedModule.induction_on with
    | h m s =>
      simp only [ModuleCat.coe_comp, Function.comp_apply, localizationToStalk_mk',
        ModuleCat.id_apply]
      erw [stalkToFiberLinearMap_germ']
      simp

end Tilde

end ModuleCat
