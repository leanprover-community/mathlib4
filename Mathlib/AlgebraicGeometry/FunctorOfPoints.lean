/-
Copyright (c) 2024 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import Mathlib.AlgebraicGeometry.OpenImmersion
import Mathlib.AlgebraicGeometry.Gluing
import Mathlib.AlgebraicGeometry.GammaSpecAdjunction

/-!

# The functor of points

-/

noncomputable section

namespace AlgebraicGeometry

universe u

open CategoryTheory


@[simps! obj map]
def Scheme.functorOfPoints (X : Scheme.{u}) : CommRingCat.{u} ‚•§ Type u :=
  Spec.rightOp ‚ãô yoneda.obj X

@[simps! app]
def Scheme.mapFunctorOfPoints {X Y : Scheme.{u}} (f : X ‚ü∂ Y) :
    X.functorOfPoints ‚ü∂ Y.functorOfPoints :=
  whiskerLeft _ <| yoneda.map f

@[simp]
lemma Scheme.mapFunctorOfPoints_id (X : Scheme.{u}) :
    mapFunctorOfPoints (ùüô X) = ùüô _ :=
  whiskerLeft_id _

@[simp]
lemma Scheme.mapFunctorOfPoints_comp {X Y Z : Scheme.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    mapFunctorOfPoints (f ‚â´ g) = mapFunctorOfPoints f ‚â´ mapFunctorOfPoints g :=
  by simp [mapFunctorOfPoints]

@[simps]
def schemeToFunctor : Scheme.{u} ‚•§ CommRingCat.{u} ‚•§ Type u where
  obj X := X.functorOfPoints
  map f := Scheme.mapFunctorOfPoints f

instance : Faithful schemeToFunctor where
  map_injective := by
    intro X Y f g h
    let ùì§ := X.affineOpenCover
    apply ùì§.openCover.hom_ext
    intro j
    exact congr_arg (fun e => e.app (ùì§.obj j) (ùì§.map j)) h

def homOfFunctorOfPoints {X Y : Scheme.{u}} (f : X.functorOfPoints ‚ü∂ Y.functorOfPoints) :
    X ‚ü∂ Y :=
  X.affineOpenCover.openCover.glueMorphisms (fun j => f.app _ <| X.affineOpenCover.map _) <| by
    intro i j
    apply schemeToFunctor.map_injective ; ext A e : 3
    dsimp at e ‚ä¢
    let ùì§ := X.affineOpenCover
    obtain ‚ü®fst',hfst‚ü© := Scheme.Spec.map_surjective
      (e ‚â´ (Limits.pullback.fst : Limits.pullback (ùì§.map i) (ùì§.map j) ‚ü∂ _))
    obtain ‚ü®snd',hsnd‚ü© := Scheme.Spec.map_surjective
      (e ‚â´ (Limits.pullback.snd : Limits.pullback (ùì§.map i) (ùì§.map j) ‚ü∂ _))
    slice_lhs 1 2 => erw [‚Üê hfst]
    slice_rhs 1 2 => erw [‚Üê hsnd]
    have hi := congr_fun (f.naturality fst'.unop) (ùì§.map i)
    have hj := congr_fun (f.naturality snd'.unop) (ùì§.map j)
    dsimp at hi hj
    rw [‚Üê hi, ‚Üê hj]
    simp_rw [hfst, hsnd, Category.assoc, Limits.pullback.condition]

def Scheme.OpenCover.hom_ext_refinement
    {X Y : Scheme.{u}}
    (ùì§ : X.OpenCover) (ùì• : Y.OpenCover)
    (f g : X ‚ü∂ Y)
    (a : ùì§ ‚ü∂ ùì•.pullbackCover f) (b : ùì§ ‚ü∂ ùì•.pullbackCover g)
    (h : ‚àÄ j : ùì§.J,
      a.app j ‚â´ Limits.pullback.snd ‚â´ ùì•.map _ =
      b.app j ‚â´ Limits.pullback.snd ‚â´ ùì•.map _ ) :
    f = g := by
  apply ùì§.hom_ext
  intro j
  specialize h j
  have ha := a.w j
  have hb := b.w j
  dsimp at ha hb
  conv_lhs => rw [‚Üê ha, Category.assoc, Limits.pullback.condition]
  conv_rhs => rw [‚Üê hb, Category.assoc, Limits.pullback.condition]
  exact h

instance : Full schemeToFunctor where
  preimage f := homOfFunctorOfPoints f
  witness := by
    intro X Y f
    ext A e : 3
    dsimp [homOfFunctorOfPoints] at e ‚ä¢
    let ùì§ := X.affineCover
    let ùì• := ùì§.pullbackCover e
    let ùì¶ := ùì•.affineRefinement
    let Œπ : ùì¶.openCover ‚ü∂ ùì• := ùì•.fromAffineRefinement
    apply ùì¶.openCover.hom_ext
    intro j
    dsimp
    have aux : ùì¶.map j ‚â´ e = Œπ.app j ‚â´ Limits.pullback.snd ‚â´ X.affineCover.map j.fst := by
      have := Œπ.w j
      dsimp at this
      rw [‚Üê this]
      rw [Category.assoc]
      congr 1
      apply Limits.pullback.condition
    rw [reassoc_of% aux, Scheme.OpenCover.Œπ_glueMorphisms]
    let ‚ü®w,hw‚ü© := Scheme.Spec.map_surjective (ùì¶.map j)
    have := congr_fun (f.naturality w.unop) e
    dsimp at this
    rw [‚Üê hw, ‚Üê this, hw, aux]
    let ‚ü®w,hw‚ü© := Scheme.Spec.map_surjective (Œπ.app j ‚â´ Limits.pullback.snd)
    simp only [‚Üê Category.assoc, ‚Üê hw]
    exact congr_fun (f.naturality w.unop) (X.affineCover.map j.fst) |>.symm

end AlgebraicGeometry
