/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang, Christian Merten
-/
import Mathlib.Algebra.Category.Ring.FinitePresentation
import Mathlib.AlgebraicGeometry.IdealSheaf.Functorial
import Mathlib.AlgebraicGeometry.Morphisms.Separated
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.Connected
import Mathlib.CategoryTheory.Monad.Limits

/-!

# Inverse limits of schemes with affine transition maps

In this file, we develop API for inverse limits of schemes with affine transition maps,
following EGA IV 8 and https://stacks.math.columbia.edu/tag/01YT.

-/

universe uI u

open CategoryTheory Limits

namespace AlgebraicGeometry

-- We refrain from considering diagrams in the over category since inverse limits in the over
-- category is isomorphic to limits in `Scheme`. Instead we use `D âŸ¶ (Functor.const I).obj S` to
-- say that the diagram is over the base scheme `S`.
variable {I : Type u} [Category.{u} I] {S X : Scheme.{u}} (D : I â¥¤ Scheme.{u})
  (t : D âŸ¶ (Functor.const I).obj S) (f : X âŸ¶ S) (c : Cone D) (hc : IsLimit c)

include hc in
/--
Suppose we have a cofiltered diagram of nonempty quasi-compact schemes,
whose transition maps are affine. Then the limit is also nonempty.
-/
@[stacks 01Z2]
lemma Scheme.nonempty_of_isLimit [IsCofilteredOrEmpty I]
    [âˆ€ {i j} (f : i âŸ¶ j), IsAffineHom (D.map f)] [âˆ€ i, Nonempty (D.obj i)]
    [âˆ€ i, CompactSpace (D.obj i)] :
    Nonempty c.pt := by
  classical
  cases isEmpty_or_nonempty I
  Â· have e := (isLimitEquivIsTerminalOfIsEmpty _ _ hc).uniqueUpToIso specULiftZIsTerminal
    exact Nonempty.map e.inv inferInstance
  Â· have i := Nonempty.some â€¹Nonempty Iâ€º
    have : IsCofiltered I := âŸ¨âŸ©
    let ğ’° := (D.obj i).affineCover.finiteSubcover
    have (i' : _) : IsAffine (ğ’°.X i') := inferInstanceAs (IsAffine (Spec _))
    obtain âŸ¨j, HâŸ© :
        âˆƒ j : ğ’°.Iâ‚€, âˆ€ {i'} (f : i' âŸ¶ i), Nonempty ((ğ’°.pullbackâ‚ (D.map f)).X j) := by
      by_contra! H
      choose i' f hf using H
      let g (j) := IsCofiltered.infTo (insert i (Finset.univ.image i'))
        (Finset.univ.image fun j : ğ’°.Iâ‚€ â†¦ âŸ¨_, _, by simp, by simp, f jâŸ©) (X := j)
      have (j : ğ’°.Iâ‚€) : IsEmpty ((ğ’°.pullbackâ‚ (D.map (g i (by simp)))).X j) := by
        let F : (ğ’°.pullbackâ‚ (D.map (g i (by simp)))).X j âŸ¶
            (ğ’°.pullbackâ‚ (D.map (f j))).X j :=
          pullback.map _ _ _ _ (D.map (g _ (by simp))) (ğŸ™ _) (ğŸ™ _) (by
            rw [â† D.map_comp, IsCofiltered.infTo_commutes]
            Â· simp [g]
            Â· simp
            Â· exact Finset.mem_image_of_mem _ (Finset.mem_univ _)) (by simp)
        exact Function.isEmpty F
      obtain âŸ¨x, -âŸ© :=
        Cover.covers (ğ’°.pullbackâ‚ (D.map (g i (by simp)))) (Nonempty.some inferInstance)
      exact (this _).elim x
    let F := Over.post D â‹™ Over.pullback (ğ’°.f j) â‹™ Over.forget _
    have (i' : _) : IsAffine (F.obj i') :=
      have : IsAffineHom (pullback.snd (D.map i'.hom) (ğ’°.f j)) :=
        MorphismProperty.pullback_snd _ _ inferInstance
      isAffine_of_isAffineHom (pullback.snd (D.map i'.hom) (ğ’°.f j))
    have (i' : _) : Nonempty (F.obj i') := H i'.hom
    let e : F âŸ¶ (F â‹™ Scheme.Î“.rightOp) â‹™ Scheme.Spec := Functor.whiskerLeft F Î“Spec.adjunction.unit
    have (i : _) : IsIso (e.app i) := IsAffine.affine
    have : IsIso e := NatIso.isIso_of_isIso_app e
    let c' : LimitCone F := âŸ¨_, (IsLimit.postcomposeInvEquiv (asIso e) _).symm
      (isLimitOfPreserves Scheme.Spec (limit.isLimit (F â‹™ Scheme.Î“.rightOp)))âŸ©
    have : Nonempty c'.1.pt := by
      apply (config := { allowSynthFailures := true }) PrimeSpectrum.instNonemptyOfNontrivial
      have (i' : _) : Nontrivial ((F â‹™ Scheme.Î“.rightOp).leftOp.obj i') := by
        apply (config := { allowSynthFailures := true }) Scheme.component_nontrivial
        simp
      exact CommRingCat.FilteredColimits.nontrivial
        (isColimitCoconeLeftOpOfCone _ (limit.isLimit (F â‹™ Scheme.Î“.rightOp)))
    let Î± : F âŸ¶ Over.forget _ â‹™ D := Functor.whiskerRight
      (Functor.whiskerLeft (Over.post D) (Over.mapPullbackAdj (ğ’°.f j)).counit) (Over.forget _)
    exact this.map (((Functor.Initial.isLimitWhiskerEquiv (Over.forget i) c).symm hc).lift
        ((Cones.postcompose Î±).obj c'.1))

include hc in
open Scheme.IdealSheafData in
/--
Suppose we have a cofiltered diagram of schemes whose transition maps are affine. The limit of
a family of compatible nonempty quasicompact closed sets in the diagram is also nonempty.
-/
lemma exists_mem_of_isClosed_of_nonempty
    [IsCofilteredOrEmpty I]
    [âˆ€ {i j} (f : i âŸ¶ j), IsAffineHom (D.map f)]
    (Z : âˆ€ (i : I), Set (D.obj i))
    (hZc : âˆ€ (i : I), IsClosed (Z i))
    (hZne : âˆ€ i, (Z i).Nonempty)
    (hZcpt : âˆ€ i, IsCompact (Z i))
    (hmapsTo : âˆ€ {i i' : I} (f : i âŸ¶ i'), Set.MapsTo (D.map f) (Z i) (Z i')) :
    âˆƒ (s : c.pt), âˆ€ i, c.Ï€.app i s âˆˆ Z i := by
  let D' : I â¥¤ Scheme :=
  { obj i := (vanishingIdeal âŸ¨Z i, hZc iâŸ©).subscheme
    map {X Y} f := subschemeMap _ _ (D.map f) (by
      rw [map_vanishingIdeal, â† le_support_iff_le_vanishingIdeal]
      simpa [(hZc _).closure_subset_iff] using (hmapsTo f).subset_preimage)
    map_id _ := by simp [â† cancel_mono (subschemeÎ¹ _)]
    map_comp _ _ := by simp [â† cancel_mono (subschemeÎ¹ _)] }
  let Î¹ : D' âŸ¶ D := { app i := subschemeÎ¹ _, naturality _ _ _ := by simp [D'] }
  haveI {i j} (f : i âŸ¶ j) : IsAffineHom (D'.map f) := by
    suffices IsAffineHom (D'.map f â‰« Î¹.app j) from .of_comp _ (Î¹.app j)
    simp only [subschemeMap_subschemeÎ¹, D', Î¹]
    infer_instance
  haveI _ (i) : Nonempty (D'.obj i) := Set.nonempty_coe_sort.mpr (hZne i)
  haveI _ (i) : CompactSpace (D'.obj i) := isCompact_iff_compactSpace.mp (hZcpt i)
  let c' : Cone D' :=
  { pt := (â¨† i, (vanishingIdeal âŸ¨Z i, hZc iâŸ©).comap (c.Ï€.app i)).subscheme
    Ï€ :=
    { app i := subschemeMap _ _ (c.Ï€.app i) (by simp [le_map_iff_comap_le, le_iSup_of_le i])
      naturality {i j} f := by simp [D', â† cancel_mono (subschemeÎ¹ _)] } }
  let hc' : IsLimit c' :=
  { lift s := IsClosedImmersion.lift (subschemeÎ¹ _) (hc.lift ((Cones.postcompose Î¹).obj s)) (by
      suffices âˆ€ i, vanishingIdeal âŸ¨Z i, hZc iâŸ© â‰¤ (s.Ï€.app i â‰« Î¹.app i).ker by
        simpa [â† le_map_iff_comap_le, â† Scheme.Hom.ker_comp]
      refine fun i â†¦ .trans ?_ (Scheme.Hom.le_ker_comp _ _)
      simp [Î¹])
    fac s i := by simp [â† cancel_mono (subschemeÎ¹ _), c', Î¹]
    uniq s m hm := by
      rw [â† cancel_mono (subschemeÎ¹ _)]
      refine hc.hom_ext fun i â†¦ ?_
      simp [Î¹, c', â† hm] }
  have : Nonempty (â¨† i, (vanishingIdeal âŸ¨Z i, hZc iâŸ©).comap (c.Ï€.app i)).support :=
    Scheme.nonempty_of_isLimit D' c' hc'
  simpa using this

include hc in
/--
A variant of `exists_mem_of_isClosed_of_nonempty` where the closed sets are only defined
for the objects over a given `j : I`.
-/
@[stacks 01Z3]
lemma exists_mem_of_isClosed_of_nonempty'
    [IsCofilteredOrEmpty I]
    [âˆ€ {i j} (f : i âŸ¶ j), IsAffineHom (D.map f)]
    {j : I}
    (Z : âˆ€ (i : I), (i âŸ¶ j) â†’ Set (D.obj i))
    (hZc : âˆ€ i hij, IsClosed (Z i hij))
    (hZne : âˆ€ i hij, (Z i hij).Nonempty)
    (hZcpt : âˆ€ i hij, IsCompact (Z i hij))
    (hstab : âˆ€ (i i' : I) (hi'i : i' âŸ¶ i) (hij : i âŸ¶ j),
      Set.MapsTo (D.map hi'i) (Z i' (hi'i â‰« hij)) (Z i hij)) :
    âˆƒ (s : c.pt), âˆ€ i hij, c.Ï€.app i s âˆˆ Z i hij := by
  have {iâ‚ iâ‚‚ : Over j} (f : iâ‚ âŸ¶ iâ‚‚) : IsAffineHom ((Over.forget j â‹™ D).map f) := by
    dsimp; infer_instance
  simpa [Over.forall_iff] using exists_mem_of_isClosed_of_nonempty (Over.forget j â‹™ D) _
    ((Functor.Initial.isLimitWhiskerEquiv (Over.forget j) c).symm hc)
    (fun i â†¦ Z i.left i.hom) (fun _ â†¦ hZc _ _)  (fun _ â†¦ hZne _ _)  (fun _ â†¦ hZcpt _ _)
    (fun {iâ‚ iâ‚‚} f â†¦ by dsimp; rw [â† Over.w f]; exact hstab ..)

section Opens

include hc in
/-- Let `{ Dáµ¢ }` be a cofiltered diagram of compact schemes with affine transition maps.
If `U âŠ† Dâ±¼` contains the image of `limáµ¢ Dáµ¢ âŸ¶ Dâ±¼`, then it contains the image of some `Dâ‚– âŸ¶ Dâ±¼`. -/
lemma exists_map_eq_top
    [IsCofiltered I]
    [âˆ€ {i j} (f : i âŸ¶ j), IsAffineHom (D.map f)]
    [âˆ€ i, CompactSpace (D.obj i)]
    {i : I} (U : (D.obj i).Opens) (hU : c.Ï€.app i â»Â¹áµ U = âŠ¤) :
    âˆƒ (j : I) (fji : j âŸ¶ i), D.map fji â»Â¹áµ U = âŠ¤ := by
  by_contra! H
  obtain âŸ¨s, hsâŸ© := exists_mem_of_isClosed_of_nonempty' D c hc (fun j f â†¦ (D.map f â»Â¹áµ U)á¶œ)
    (fun j f â†¦ (D.map f â»Â¹áµ U).2.isClosed_compl) (fun j f â†¦ by
      simp only [TopologicalSpace.Opens.map_coe, Set.nonempty_compl, ne_eq]
      exact SetLike.coe_injective.ne (H j f))
    (fun j f â†¦ (D.map f â»Â¹áµ U).2.isClosed_compl.isCompact)
    (fun j k fkj fji x (hx : _ âˆ‰ U) â†¦ by rwa [Functor.map_comp] at hx)
  exact absurd (hU.ge (Set.mem_univ s)) (by simpa using hs i (ğŸ™ i))

attribute [local simp] Scheme.Hom.resLE_comp_resLE

/-- Given a diagram `{ Dáµ¢ }` of schemes and a open `U âŠ† Dáµ¢`,
this is the diagram of `{ Dâ±¼áµ¢â»Â¹ U }_{j â‰¤ i}`. -/
@[simps] noncomputable
def opensDiagram (i : I) (U : (D.obj i).Opens) : Over i â¥¤ Scheme where
  obj j := D.map j.hom â»Â¹áµ U
  map {j k} f := (D.map f.left).resLE _ _
    (by rw [â† Scheme.Hom.comp_preimage, â† D.map_comp, Over.w f])

/-- The map `Dâ±¼áµ¢â»Â¹ U âŸ¶ Dáµ¢` from the restricted diagram to the original diagram. -/
@[simps] noncomputable
def opensDiagramÎ¹ (i : I) (U : (D.obj i).Opens) : opensDiagram D i U âŸ¶ Over.forget _ â‹™ D where
  app j := Scheme.Opens.Î¹ _

instance (i : I) (U : (D.obj i).Opens) (j : Over i) :
    IsOpenImmersion ((opensDiagramÎ¹ D i U).app j) := by
  delta opensDiagramÎ¹; infer_instance

/-- Given a diagram `{ Dáµ¢ }` of schemes and a open `U âŠ† Dáµ¢`,
the preimage of `U âŠ† Dáµ¢` under the map `lim Dáµ¢ âŸ¶ Dáµ¢` is the limit of `{ Dâ±¼áµ¢â»Â¹ U }_{j â‰¤ i}`.
This is the underlying cone, and it is limiting as witnessed by `isLimitOpensCone` below. -/
@[simps] noncomputable
def opensCone (i : I) (U : (D.obj i).Opens) : Cone (opensDiagram D i U) where
  pt := c.Ï€.app i â»Â¹áµ U
  Ï€.app j := (c.Ï€.app j.left).resLE _ _ (by rw [â† Scheme.Hom.comp_preimage, c.w]; rfl)

attribute [local instance] CategoryTheory.isConnected_of_hasTerminal

/-- Given a diagram `{ Dáµ¢ }_{i âˆˆ I}` of schemes and a open `U âŠ† Dáµ¢`,
the preimage of `U âŠ† Dáµ¢` under the map `lim Dáµ¢ âŸ¶ Dáµ¢` is the limit of `{ Dâ±¼áµ¢â»Â¹ U }_{j â‰¤ i}`. -/
noncomputable
def isLimitOpensCone [IsCofiltered I] (i : I) (U : (D.obj i).Opens) :
    IsLimit (opensCone D c i U) :=
  isLimitOfIsPullbackOfIsConnected (opensDiagramÎ¹ D i U) _ _
    (by exact { hom := (c.Ï€.app i â»Â¹áµ U).Î¹ })
    (fun j â†¦ IsOpenImmersion.isPullback _ _ _ _ (by simp) (by simp [â† Scheme.Hom.comp_preimage]))
    ((Functor.Initial.isLimitWhiskerEquiv (Over.forget i) c).symm hc)

instance [âˆ€ {i j} (f : i âŸ¶ j), IsAffineHom (D.map f)] {i : I}
    (U : (D.obj i).Opens) {j k : Over i} (f : j âŸ¶ k) :
    IsAffineHom ((opensDiagram D i U).map f) := by
  refine âŸ¨fun V hV â†¦ ?_âŸ©
  convert ((hV.image_of_isOpenImmersion (D.map k.hom â»Â¹áµ U).Î¹).preimage
    (D.map f.left)).preimage_of_isOpenImmersion (D.map j.hom â»Â¹áµ U).Î¹ ?_
  Â· ext x
    change _ âˆˆ V â†” _
    refine âŸ¨fun h â†¦ âŸ¨âŸ¨(D.map f.left).base x.1, ?_âŸ©, ?_, rflâŸ©, ?_âŸ©
    Â· change (D.map f.left â‰« D.map k.hom).base x.1 âˆˆ U
      rw [â† D.map_comp, Over.w f]
      exact x.2
    Â· convert h
      exact Subtype.ext (by simp)
    Â· rintro âŸ¨âŸ¨_, hUâŸ©, hV, rflâŸ©
      convert hV
      exact Subtype.ext (by simp)
  Â· simp only [Functor.id_obj, opensDiagram_obj, Functor.const_obj_obj,
      Scheme.Opens.opensRange_Î¹]
    rintro x âŸ¨âŸ¨y, hâ‚ : (D.map k.hom).base y âˆˆ UâŸ©, hâ‚‚, eâŸ©
    obtain rfl : y = (D.map f.left).base x := congr($e)
    dsimp at hâ‚
    rw [â† Scheme.Hom.comp_apply] at hâ‚
    rwa [â† D.map_comp, Over.w f] at hâ‚

end Opens

include hc in
lemma isAffineHom_Ï€_app [IsCofiltered I] [âˆ€ {i j} (f : i âŸ¶ j), IsAffineHom (D.map f)] (i : I) :
    IsAffineHom (c.Ï€.app i) where
  isAffine_preimage U hU := have (j : _) : IsAffine ((opensDiagram D i U).obj j) := hU.preimage _
    Scheme.isAffine_of_isLimit _ (isLimitOpensCone D c hc i U)

include hc in
lemma Scheme.compactSpace_of_isLimit [IsCofiltered I]
    [âˆ€ {i j} (f : i âŸ¶ j), IsAffineHom (D.map f)] [âˆ€ i, CompactSpace (D.obj i)] :
    CompactSpace c.pt := by
  obtain âŸ¨iâŸ© := IsCofiltered.nonempty (C := I)
  have := isAffineHom_Ï€_app _ _ hc
  exact QuasiCompact.compactSpace_of_compactSpace (c.Ï€.app i)

/-!

# Cofiltered Limits and Schemes of Finite Type

Given a cofiltered diagram `D` of quasi-compact `S`-schemes with affine transition maps,
and another scheme `X` of finite type over `S`.
Then the canonical map `colim Homâ‚›(Dáµ¢, X) âŸ¶ Homâ‚›(lim Dáµ¢, X)` is injective.
In other words, for each pair of `a : Homâ‚›(Dáµ¢, X)` and `b : Homâ‚›(Dâ±¼, X)` that give rise to the
same map `Homâ‚›(lim Dáµ¢, X)`, there exists a `k` with `fáµ¢ : k âŸ¶ i` and `fâ±¼ : k âŸ¶ j` such that
`D(fáµ¢) â‰« a = D(fâ±¼) â‰« b`.
This results is formalized in `Scheme.exists_hom_hom_comp_eq_comp_of_locallyOfFiniteType`.

We first reduce to the case `i = j`, and the goal is to reduce to the case where `X` and `S`
are affine, where the result follows from commutative algebra.

To achieve this, we show that there exists some `iâ‚€ âŸ¶ i` such that for each `x`, `a x` and `b x`
are contained in the same component (i.e. given an open cover `ğ’°â‚›` of `S`,
and `ğ’°â‚“` of `X` refining `ğ’°â‚›`, the range of `x â†¦ (a x, b x)` falls in the diagonal part
`â‹ƒáµ¢â±¼ ğ’°â‚“â±¼ Ã—[ğ’°â‚›áµ¢] ğ’°â‚“â±¼`).
Then we may restrict to the sub-diagram over `iâ‚€` (which is cofinal because `D` is cofiltered),
and check locally on `X`, reducing to the affine case.

For the actual implementation, we wrap `i`, `a`, `b`, the limit cone `lim Dáµ¢`, and open covers
of `X` and `S` into a structure `ExistsHomHomCompEqCompAux` for convenience.

See the injective part of (1) => (3) of https://stacks.math.columbia.edu/tag/01ZC.
-/

section LocallyOfFiniteType

variable [âˆ€ i, CompactSpace (D.obj i)] [LocallyOfFiniteType f] [IsCofiltered I]

include hc in
/-- Subsumed by `Scheme.exists_hom_hom_comp_eq_comp_of_locallyOfFiniteType`. -/
private nonrec lemma Scheme.exists_hom_hom_comp_eq_comp_of_isAffine_of_locallyOfFiniteType
    [IsAffine S] [IsAffine X] [âˆ€ i, IsAffine (D.obj i)] [IsAffine c.pt]
    {i : I} (a : D.obj i âŸ¶ X) (ha : t.app i = a â‰« f)
    {j : I} (b : D.obj j âŸ¶ X) (hb : t.app j = b â‰« f)
    (hab : c.Ï€.app i â‰« a = c.Ï€.app j â‰« b) :
    âˆƒ (k : I) (hik : k âŸ¶ i) (hjk : k âŸ¶ j),
      D.map hik â‰« a = D.map hjk â‰« b := by
  wlog hS : âˆƒ R, S = Spec R generalizing S
  Â· exact this (t â‰« ((Functor.const I).mapIso S.isoSpec).hom)
      (f â‰« S.isoSpec.hom) (by simp [ha]) (by simp [hb]) âŸ¨_, rflâŸ©
  obtain âŸ¨R, rflâŸ© := hS
  wlog hX : âˆƒ S, X = Spec S generalizing X
  Â· simpa using this (a â‰« X.isoSpec.hom) (b â‰« X.isoSpec.hom) (by simp [hab]) (X.isoSpec.inv â‰« f)
      (by simp [ha]) (by simp [hb]) âŸ¨_, rflâŸ©
  obtain âŸ¨S, rflâŸ© := hX
  obtain âŸ¨Ï†, rflâŸ© := Spec.map_surjective f
  wlog hD : âˆƒ D' : I â¥¤ CommRingCatáµ’áµ–, D = D' â‹™ Scheme.Spec generalizing D
  Â· let e : D âŸ¶ D â‹™ Scheme.Î“.rightOp â‹™ Scheme.Spec := D.whiskerLeft Î“Spec.adjunction.unit
    have inst (i) : IsIso (e.app i) := by dsimp [e]; infer_instance
    have inst : IsIso e := NatIso.isIso_of_isIso_app e
    have inst (i) : IsAffine ((D â‹™ Scheme.Î“.rightOp â‹™ Scheme.Spec).obj i) := by
      dsimp; infer_instance
    have inst : IsAffine ((Cones.postcompose (asIso e).hom).obj c).pt := by
      dsimp; infer_instance
    have := this (D â‹™ Scheme.Î“.rightOp â‹™ Scheme.Spec) ((Cones.postcompose (asIso e).hom).obj c)
      ((IsLimit.postcomposeHomEquiv (asIso e) c).symm hc) (inv e â‰« t)
      ((inv e).app _ â‰« a) ((inv e).app _ â‰« b) (by simp [hab]) (by simp [ha]) (by simp [hb])
      âŸ¨D â‹™ Scheme.Î“.rightOp, rflâŸ©
    simp_rw [(inv e).naturality_assoc] at this
    simpa using this
  obtain âŸ¨D, rflâŸ© := hD
  obtain âŸ¨a, rflâŸ© := Spec.map_surjective a
  obtain âŸ¨b, rflâŸ© := Spec.map_surjective b
  let e : ((Functor.const Iáµ’áµ–).obj R).rightOp â‹™ Scheme.Spec â‰… (Functor.const I).obj (Spec R) :=
    NatIso.ofComponents (fun _ â†¦ Iso.refl _) (by simp)
  obtain âŸ¨t, rflâŸ© : âˆƒ t' : (Functor.const Iáµ’áµ–).obj R âŸ¶ D.leftOp,
      t = Functor.whiskerRight (NatTrans.rightOp t') Scheme.Spec â‰« e.hom :=
    âŸ¨âŸ¨fun i â†¦ Spec.preimage (t.app i.unop), fun _ _ f â†¦ Spec.map_injective
      (by simpa using (t.naturality f.unop).symm)âŸ©, by ext : 2; simp [e]âŸ©
  have := monadicCreatesLimits Scheme.Spec
  obtain âŸ¨k, hik, hjk, HâŸ© := (HasRingHomProperty.Spec_iff.mp â€¹LocallyOfFiniteType (Spec.map Ï†)â€º)
    |>.essFiniteType.exists_comp_map_eq_of_isColimit _ D.leftOp t _
    (coconeLeftOpOfCone (liftLimit hc))
    (isColimitCoconeLeftOpOfCone _ (liftedLimitIsLimit _))
    a (Spec.map_injective (by simpa using ha.symm))
    b (Spec.map_injective (by simpa using hb.symm))
    (Spec.map_injective (by
      simp only [coconeLeftOpOfCone_pt, Functor.const_obj_obj,
        Functor.leftOp_obj, coconeLeftOpOfCone_Î¹_app, Spec.map_comp]
      simp only [â† Scheme.Spec_map, â† liftedLimitMapsToOriginal_hom_Ï€, Category.assoc, hab]))
  exact âŸ¨k.unop, hik.unop, hjk.unop, by simpa [â† Spec.map_comp, Spec.map_inj] using HâŸ©

/-- (Implementation)
An auxiliary structure used to prove `Scheme.exists_hom_hom_comp_eq_comp_of_locallyOfFiniteType`.
See the section docstring. -/
structure ExistsHomHomCompEqCompAux where
  /-- (Implementation) The limit cone. See the section docstring. -/
  c : Cone D
  /-- (Implementation) The limit cone is a limit. See the section docstring. -/
  hc : IsLimit c
  /-- (Implementation) The index on which `a` and `b` lives. See the section docstring. -/
  i : I
  /-- (Implementation) `a`. See the section docstring. -/
  a : D.obj i âŸ¶ X
  ha : t.app i = a â‰« f
  /-- (Implementation) `b`. See the section docstring. -/
  b : D.obj i âŸ¶ X
  hb : t.app i = b â‰« f
  hab : c.Ï€.app i â‰« a = c.Ï€.app i â‰« b
  /-- (Implementation) An open cover on `S`. See the section docstring. -/
  ğ’°S : Scheme.OpenCover.{u} S
  [hğ’°S : âˆ€ i, IsAffine (ğ’°S.X i)]
  /-- (Implementation) A family of open covers refining `ğ’°S`. See the section docstring. -/
  ğ’°X (i : (ğ’°S.pullbackâ‚ f).Iâ‚€) : Scheme.OpenCover.{u} ((ğ’°S.pullbackâ‚ f).X i)
  [hğ’°X : âˆ€ i j, IsAffine ((ğ’°X i).X j)]

attribute [instance] ExistsHomHomCompEqCompAux.hğ’°S ExistsHomHomCompEqCompAux.hğ’°X

namespace ExistsHomHomCompEqCompAux

noncomputable section

variable {D t f c} [âˆ€ {i j : I} (f : i âŸ¶ j), IsAffineHom (D.map f)]
variable (A : ExistsHomHomCompEqCompAux D t f)

omit [LocallyOfFiniteType f] in
lemma exists_index : âˆƒ (i' : I) (hii' : i' âŸ¶ A.i),
    ((D.map hii' â‰« pullback.lift A.a A.b (A.ha.symm.trans A.hb)) â»Â¹'
      ((Scheme.Pullback.diagonalCoverDiagonalRange f A.ğ’°S A.ğ’°X : Set <|
        â†‘(pullback f f))á¶œ)) = âˆ… := by
  let W := Scheme.Pullback.diagonalCoverDiagonalRange f A.ğ’°S A.ğ’°X
  by_contra! h
  let Z (i' : I) (hii' : i' âŸ¶ A.i) :=
    (D.map hii' â‰« pullback.lift A.a A.b (A.ha.symm.trans A.hb)) â»Â¹' Wá¶œ
  have hZ (i') (hii' : i' âŸ¶ A.i) : IsClosed (Z i' hii') :=
    (W.isOpen.isClosed_compl).preimage <| Scheme.Hom.continuous _
  obtain âŸ¨s, hsâŸ© := exists_mem_of_isClosed_of_nonempty' D A.c A.hc Z hZ h
    (fun _ _ â†¦ (hZ _ _).isCompact) (fun i i' hii' hij â†¦ by simp [Z, Set.MapsTo])
  refine hs A.i (ğŸ™ A.i) (Scheme.Pullback.range_diagonal_subset_diagonalCoverDiagonalRange _ _ _ ?_)
  use (A.c.Ï€.app A.i â‰« A.a) s
  have H : A.c.Ï€.app A.i â‰« A.a â‰« pullback.diagonal f =
      A.c.Ï€.app A.i â‰« pullback.lift A.a A.b (A.ha.symm.trans A.hb) := by ext <;> simp [hab]
  simp [â† Scheme.Hom.comp_apply, - Scheme.Hom.comp_base, H]

/-- (Implementation)
The index `i'` such that `a` and `b` restricted onto `i'` maps into the diagonal components.
See the section docstring. -/
def i' : I := A.exists_index.choose

/-- (Implementation) The map from `i'` to `i`. See the section docstring. -/
def hii' : A.i' âŸ¶ A.i := A.exists_index.choose_spec.choose

/-- (Implementation)
The map sending `x` to `(a x, b x)`, which should fall in the diagonal component.
See the section docstring. -/
def g : D.obj A.i' âŸ¶ pullback f f :=
  (D.map A.hii' â‰« pullback.lift A.a A.b (A.ha.symm.trans A.hb))

omit [LocallyOfFiniteType f] in
lemma range_g_subset :
    Set.range A.g âŠ† Scheme.Pullback.diagonalCoverDiagonalRange f A.ğ’°S A.ğ’°X := by
  simpa [ExistsHomHomCompEqCompAux.hii', g] using A.exists_index.choose_spec.choose_spec

/-- (Implementation)
The covering of `D(i')` by the pullback of the diagonal components of `X Ã—â‚› X`.
See the section docstring. -/
noncomputable def ğ’°Dâ‚€ : Scheme.OpenCover.{u} (D.obj A.i') :=
  Scheme.Cover.mkOfCovers (Î£ i : A.ğ’°S.Iâ‚€, (A.ğ’°X i).Iâ‚€) _
    (fun i â†¦ ((Scheme.Pullback.diagonalCover f A.ğ’°S A.ğ’°X).pullbackâ‚ A.g).f âŸ¨i.1, i.2, i.2âŸ©)
    (fun x â†¦ by simpa [â† Set.mem_range, Scheme.Pullback.range_fst,
        Scheme.Pullback.diagonalCoverDiagonalRange] using A.range_g_subset âŸ¨x, rflâŸ©)

/-- (Implementation) An affine open cover refining `ğ’°Dâ‚€`. See the section docstring. -/
noncomputable def ğ’°D : Scheme.OpenCover.{u} (D.obj A.i') :=
  A.ğ’°Dâ‚€.bind fun _ â†¦ Scheme.affineCover _

attribute [-simp] cast_eq eq_mpr_eq_cast

/-- (Implementation) The diagram restricted to `Over i'`. See the section docstring. -/
def D' (j : A.ğ’°D.Iâ‚€) : Over A.i' â¥¤ Scheme :=
  Over.post D â‹™ Over.pullback (A.ğ’°D.f j) â‹™ Over.forget _

/-- (Implementation) The limit cone restricted to `Over i'`. See the section docstring. -/
def c' (j : A.ğ’°D.Iâ‚€) : Cone (A.D' j) :=
  (Over.pullback (A.ğ’°D.f j) â‹™ Over.forget _).mapCone ((Over.conePost _ _).obj A.c)

attribute [local instance] IsCofiltered.isConnected

/-- (Implementation)
The limit cone restricted to `Over i'` is still a limit because the diagram is cofiltered.
See the section docstring. -/
def hc' (j : A.ğ’°D.Iâ‚€) : IsLimit (A.c' j) :=
  isLimitOfPreserves (Over.pullback (A.ğ’°D.f j) â‹™ Over.forget _) (Over.isLimitConePost _ A.hc)

variable [âˆ€ i, IsAffineHom (A.c.Ï€.app i)]

lemma exists_eq (j : A.ğ’°D.Iâ‚€) : âˆƒ (k : I) (hki' : k âŸ¶ A.i'),
    (A.ğ’°D.pullbackâ‚ (D.map hki')).f j â‰« D.map (hki' â‰« A.hii') â‰« A.a =
      (A.ğ’°D.pullbackâ‚ (D.map hki')).f j â‰« D.map (hki' â‰« A.hii') â‰« A.b := by
  have : IsAffine (A.ğ’°D.X j) := by dsimp [ğ’°D]; infer_instance
  have (i : _) : IsAffine ((Over.post D â‹™ Over.pullback (A.ğ’°D.f j) â‹™ Over.forget _).obj i) := by
    dsimp; infer_instance
  have : IsAffine ((Over.pullback (A.ğ’°D.f j) â‹™ Over.forget (A.ğ’°D.X j)).mapCone
      ((Over.conePost D A.i').obj A.c)).pt := by
    dsimp; infer_instance
  have : LocallyOfFiniteType ((A.ğ’°X j.fst.fst).f j.fst.snd â‰« A.ğ’°S.pullbackHom f j.fst.fst) := by
    dsimp [Scheme.Cover.pullbackHom]; infer_instance
  have Hâ‚ := congr($(pullback.condition (f := A.g) (g := (Scheme.Pullback.diagonalCover f
    A.ğ’°S A.ğ’°X).f âŸ¨j.1.1, (j.1.2, j.1.2)âŸ©)) â‰« pullback.fst _ _)
  have Hâ‚‚ := congr($(pullback.condition (f := A.g) (g := (Scheme.Pullback.diagonalCover f
    A.ğ’°S A.ğ’°X).f âŸ¨j.1.1, (j.1.2, j.1.2)âŸ©)) â‰« pullback.snd _ _)
  simp only [Scheme.Pullback.openCoverOfBase_Iâ‚€, Scheme.Pullback.openCoverOfBase_X,
    Scheme.Cover.pullbackHom, Scheme.Pullback.openCoverOfLeftRight_Iâ‚€,
    g, Category.assoc, limit.lift_Ï€, PullbackCone.mk_pt, PullbackCone.mk_Ï€_app,
    Scheme.Pullback.diagonalCover_map] at Hâ‚ Hâ‚‚
  obtain âŸ¨k, hik, hjk, HâŸ© := Scheme.exists_hom_hom_comp_eq_comp_of_isAffine_of_locallyOfFiniteType
    (Over.post D â‹™ Over.pullback (A.ğ’°D.f j) â‹™ Over.forget _)
    ((Over.post D â‹™ Over.pullback (A.ğ’°D.f j)).whiskerLeft (Comma.natTrans _ _) â‰«
      (Functor.const _).map ((A.ğ’°Dâ‚€.X j.1).affineCover.f j.2 â‰«
      (Scheme.Pullback.diagonalCover f A.ğ’°S A.ğ’°X).pullbackHom _ _ â‰«
      pullback.fst _ _ â‰«
      (A.ğ’°X j.fst.fst).f j.fst.snd â‰« Scheme.Cover.pullbackHom A.ğ’°S f j.fst.fst))
    (((A.ğ’°X j.1.1).f j.1.2 â‰« A.ğ’°S.pullbackHom f j.1.1))
    ((Over.pullback (A.ğ’°D.f j) â‹™ Over.forget _).mapCone ((Over.conePost _ _).obj A.c))
    (by
      refine isLimitOfPreserves (Over.pullback (A.ğ’°D.f j) â‹™ Over.forget _) ?_
      apply isLimitOfReflects (Over.forget (D.obj A.i'))
      exact (Functor.Initial.isLimitWhiskerEquiv (Over.forget A.i') A.c).symm A.hc)
    (i := Over.mk (ğŸ™ _))
    (pullback.snd _ _ â‰« (A.ğ’°Dâ‚€.X j.1).affineCover.f j.2 â‰«
      (Scheme.Pullback.diagonalCover f A.ğ’°S A.ğ’°X).pullbackHom _ _ â‰«
      pullback.fst _ _)
    (by simp)
    (j := Over.mk (ğŸ™ _))
    (pullback.snd _ _ â‰« (A.ğ’°Dâ‚€.X j.1).affineCover.f j.2 â‰«
      (Scheme.Pullback.diagonalCover f A.ğ’°S A.ğ’°X).pullbackHom _ _ â‰«
      pullback.snd _ _)
    (by simp [pullback.condition])
    (by
      rw [â† cancel_mono ((A.ğ’°X j.1.1).f j.1.2), â† cancel_mono (pullback.fst f (A.ğ’°S.f j.1.1))]
      have Hâ‚ƒ := congr(pullback.fst (A.c.Ï€.app A.i') (A.ğ’°D.f j) â‰« $(A.hab))
      simp only [pullback.condition_assoc, ğ’°D, â† A.c.w A.hii', Category.assoc] at Hâ‚ƒ
      simpa [Scheme.Cover.pullbackHom, g, â† Hâ‚, â† Hâ‚‚, -Cone.w, -Cone.w_assoc] using Hâ‚ƒ)
  refine âŸ¨k.left, k.hom, ?_âŸ©
  simpa [â† cancel_mono ((A.ğ’°X j.1.1).f j.1.2), â† cancel_mono (pullback.fst f (A.ğ’°S.f j.1.1)),
    Scheme.Cover.pullbackHom, g, â† Hâ‚, â† Hâ‚‚, pullback.condition_assoc] using H

end

end ExistsHomHomCompEqCompAux

variable [âˆ€ i, IsAffineHom (c.Ï€.app i)] [âˆ€ {i j} (f : i âŸ¶ j), IsAffineHom (D.map f)]

include hc in
/--
Given a cofiltered diagram `D` of quasi-compact `S`-schemes with affine transition maps,
and another scheme `X` of finite type over `S`.
Then the canonical map `colim Homâ‚›(Dáµ¢, X) âŸ¶ Homâ‚›(lim Dáµ¢, X)` is injective.

In other words, for each pair of `a : Homâ‚›(Dáµ¢, X)` and `b : Homâ‚›(Dâ±¼, X)` that give rise to the
same map `Homâ‚›(lim Dáµ¢, X)`, there exists a `k` with `fáµ¢ : k âŸ¶ i` and `fâ±¼ : k âŸ¶ j` such that
`D(fáµ¢) â‰« a = D(fâ±¼) â‰« b`.
-/
@[stacks 01ZC "Injective part of (1) => (3)"]
lemma Scheme.exists_hom_hom_comp_eq_comp_of_locallyOfFiniteType
    {i : I} (a : D.obj i âŸ¶ X) (ha : t.app i = a â‰« f)
    {j : I} (b : D.obj j âŸ¶ X) (hb : t.app j = b â‰« f)
    (hab : c.Ï€.app i â‰« a = c.Ï€.app j â‰« b) :
    âˆƒ (k : I) (hik : k âŸ¶ i) (hjk : k âŸ¶ j),
      D.map hik â‰« a = D.map hjk â‰« b := by
  classical
  wlog h : i = j
  Â· let o := IsCofiltered.min i j
    have := this D t f c hc (D.map (IsCofiltered.minToLeft i j) â‰« a)
      (by simp [â† ha]) (D.map (IsCofiltered.minToRight i j) â‰« b)
      (by simp [â† hb]) (by simpa) rfl
    obtain âŸ¨k, hik, hjk, heqâŸ© := this
    use k, hik â‰« IsCofiltered.minToLeft i j, hjk â‰« IsCofiltered.minToRight i j
    simpa using heq
  subst h
  let A : ExistsHomHomCompEqCompAux D t f :=
    { c := c, hc := hc, i := i, a := a, ha := ha, b := b, hb := hb, hab := hab
      ğ’°S := S.affineCover, ğ’°X i := Scheme.affineCover _ }
  let ğ’° := Scheme.Pullback.diagonalCover f A.ğ’°S A.ğ’°X
  let W := Scheme.Pullback.diagonalCoverDiagonalRange f A.ğ’°S A.ğ’°X
  choose k hki' heq using A.exists_eq
  let ğ’°Df := A.ğ’°D.finiteSubcover
  rcases isEmpty_or_nonempty (D.obj A.i') with h | h
  Â· exact âŸ¨A.i', A.hii', A.hii', isInitialOfIsEmpty.hom_ext _ _âŸ©
  let O : Finset I := {A.i'} âˆª Finset.univ.image (fun i : ğ’°Df.Iâ‚€ â†¦ k <| A.ğ’°D.idx i.1)
  let o := Nonempty.some (inferInstanceAs <| Nonempty ğ’°Df.Iâ‚€)
  have ho : k (A.ğ’°D.idx o.1) âˆˆ O := by
    simp [O]
  obtain âŸ¨l, hl1, hl2âŸ© := IsCofiltered.inf_exists O
    (Finset.univ.image (fun i : ğ’°Df.Iâ‚€ â†¦
      âŸ¨k <| A.ğ’°D.idx i.1, A.i', by simp [O], by simp [O], hki' <| A.ğ’°D.idx i.1âŸ©))
  have (w v : ğ’°Df.Iâ‚€) :
      hl1 (by simp [O]) â‰« hki' (A.ğ’°D.idx w.1) = hl1 (by simp [O]) â‰« hki' (A.ğ’°D.idx v.1) := by
    trans hl1 (show A.i' âˆˆ O by simp [O])
    Â· exact hl2 _ _ (Finset.mem_image_of_mem _ (Finset.mem_univ _))
    Â· exact .symm <| hl2 _ _ (Finset.mem_image_of_mem _ (by simp))
  refine âŸ¨l, hl1 ho â‰« hki' _ â‰« A.hii', hl1 ho â‰« hki' _ â‰« A.hii', ?_âŸ©
  apply Cover.hom_ext (ğ’°Df.pullbackâ‚ (D.map <| hl1 ho â‰« hki' _))
  intro u
  let F : pullback (D.map (hl1 ho â‰« hki' (A.ğ’°D.idx o.1))) (ğ’°Df.f u) âŸ¶
      pullback (D.map (hki' <| A.ğ’°D.idx u.1)) (A.ğ’°D.f <| A.ğ’°D.idx u.1) :=
    pullback.map _ _ _ _ (D.map <| hl1 (by simp [O]))
      (ğŸ™ _) (ğŸ™ _) (by rw [Category.comp_id, â† D.map_comp, this]) rfl
  have hF : F â‰« pullback.fst (D.map (hki' _)) (A.ğ’°D.f _) =
      pullback.fst _ _ â‰« D.map (hl1 (by simp [O])) := by simp [F]
  simp only [Precoverage.ZeroHypercover.pullbackâ‚_toPreZeroHypercover,
    PreZeroHypercover.pullbackâ‚_X, PreZeroHypercover.pullbackâ‚_f, Functor.map_comp, Category.assoc]
    at heq âŠ¢
  simp_rw [â† D.map_comp_assoc, reassoc_of% this o u, D.map_comp_assoc]
  rw [â† reassoc_of% hF, â† reassoc_of% hF, heq]

end LocallyOfFiniteType

/-!
## Sections of the limit

Let `D` be a cofiltered diagram schemes with affine transition map.
Consider the canonical map `colim Î“(Dáµ¢, âŠ¤) âŸ¶ Î“(lim Dáµ¢, âŠ¤)`.

If `D` consists of quasicompact schemes, then this map is injective. More generally, we show
that if `s t : Î“(Dáµ¢, U)` have equal image in `lim Dáµ¢`, then they are equal at some `Î“(Dâ±¼, Dâ±¼áµ¢â»Â¹U)`.
See `AlgebraicGeometry.exists_app_map_eq_map_of_isLimit`.

If `D` consists of qcqs schemes, then this map is surjective. Specifically, we show that
any `s : Î“(lim Dáµ¢, âŠ¤)` comes from `Î“(Dáµ¢, âŠ¤)` for some `i`.
See `AlgebraicGeometry.exists_appTop_Ï€_eq_of_isLimit`.

These two results imply that `PreservesLimit D Scheme.Î“.rightOp`, which is available as an instance.

-/
section sections

variable [IsCofiltered I]

include hc in
lemma exists_appTop_map_eq_zero_of_isAffine_of_isLimit
    [âˆ€ i, IsAffine (D.obj i)]
    (i : I) (s : Î“(D.obj i, âŠ¤)) (hs : (c.Ï€.app i).appTop s = 0) :
    âˆƒ (j : I) (f : j âŸ¶ i), (D.map f).appTop s = 0 := by
  have : âˆ€ i, IsAffine (D.op.obj i).unop := by dsimp; infer_instance
  obtain âŸ¨j, f, hjâŸ© := (Types.FilteredColimit.isColimit_eq_iff'
    (isColimitOfPreserves (Scheme.Î“ â‹™ forget _) hc.op) s (0 : Î“(D.obj i, âŠ¤))).mp (by simpa)
  exact âŸ¨j.unop, f.unop, by simpa using hjâŸ©

include hc in
lemma exists_appTop_map_eq_zero_of_isLimit [âˆ€ {i j} (f : i âŸ¶ j), IsAffineHom (D.map f)]
    {i : I} [CompactSpace (D.obj i)] (s : Î“(D.obj i, âŠ¤)) (hs : (c.Ï€.app i).appTop s = 0) :
    âˆƒ (j : I) (f : j âŸ¶ i), (D.map f).appTop s = 0 := by
  classical
  have (x : D.obj i) : âˆƒ (U : (D.obj i).Opens) (hU : IsAffineOpen U)
      (hU : x âˆˆ U) (j : I) (f : j âŸ¶ i), (D.map f).app U (s |_ U) = 0 := by
    obtain âŸ¨_, âŸ¨U, hU : IsAffineOpen U, rflâŸ©, hxU, -âŸ© :=
      (D.obj i).isBasis_affineOpens.exists_subset_of_mem_open (Set.mem_univ x) isOpen_univ
    have (j : Over i) : IsAffine ((opensDiagram D i U).obj j) := hU.preimage (D.map _)
    obtain âŸ¨j, f, hjâŸ© := exists_appTop_map_eq_zero_of_isAffine_of_isLimit _ _
      (isLimitOpensCone D c hc i U) (.mk (ğŸ™ i)) (((opensDiagramÎ¹ D i U).app _).appTop s) (by
        convert congr((c.pt.presheaf.map (homOfLE le_top).op).hom $hs) using 1
        Â· simp [Scheme.Hom.app_eq_appLE, Scheme.Hom.resLE_appLE, â† ConcreteCategory.comp_apply]; rfl
        Â· simp)
    refine âŸ¨U, hU, hxU, j.left, j.hom, ?_âŸ©
    have hf : f.left = j.hom := by simpa using Over.w f
    let t' : Î“(D.map j.hom â»Â¹áµ U, âŠ¤) âŸ¶ Î“(D.obj j.left, D.map j.hom â»Â¹áµ U) :=
      (D.obj _).presheaf.map (eqToHom ((D.map j.hom â»Â¹áµ U).Î¹_image_top.symm)).op
    convert congr(t' $hj)
    Â· dsimp [TopCat.Presheaf.restrictOpen, TopCat.Presheaf.restrict]
      simp only [Scheme.Hom.app_eq_appLE, homOfLE_leOfHom, â† ConcreteCategory.comp_apply, hf,
        Scheme.Hom.map_appLE, TopologicalSpace.Opens.map_top, Scheme.Hom.resLE_appLE]
      simp [t']
    Â· simp
  choose U hU hxU j f H using this
  obtain âŸ¨t, htâŸ© := CompactSpace.elim_nhds_subcover (U Â·) (fun x â†¦ (U x).2.mem_nhds (hxU x))
  obtain âŸ¨k, fk, hkâŸ© := IsCofiltered.inf_exists (insert i <| t.image j) (by
    exact t.attach.image fun x â†¦ âŸ¨j x.1, i, Finset.mem_insert_of_mem
      (Finset.mem_image_of_mem _ x.2), by simp, f x.1âŸ©)
  refine âŸ¨k, fk (by simp), ?_âŸ©
  apply (D.obj k).IsSheaf.section_ext
  rintro x -
  obtain âŸ¨l, hl, hlUâŸ© := Set.mem_iUnionâ‚‚.mp (ht.ge (Set.mem_univ ((D.map (fk (by simp))).base x)))
  refine âŸ¨D.map (fk (by simp)) â»Â¹áµ U l, le_top, hlU, ?_âŸ©
  dsimp
  simp only [homOfLE_leOfHom, map_zero]
  have hâ‚ : fk (by simp) = fk (Finset.mem_insert_of_mem (Finset.mem_image_of_mem _ hl)) â‰« f l :=
    (hk _ (by simp) (Finset.mem_image.mpr âŸ¨âŸ¨l, hlâŸ©, by simp, by simpâŸ©)).symm
  have hâ‚‚ : D.map (fk (Finset.mem_insert_self _ _)) â»Â¹áµ U l â‰¤ D.map (fk (Finset.mem_insert_of_mem
      (Finset.mem_image_of_mem _ hl))) â»Â¹áµ D.map (f l) â»Â¹áµ U l := by
    rw [â† Scheme.Hom.comp_preimage, â† D.map_comp, hâ‚]
  convert congr((D.map (fk _)).appLE _ _ hâ‚‚ $(H l))
  Â· dsimp [TopCat.Presheaf.restrictOpen, TopCat.Presheaf.restrict]
    simp [Scheme.Hom.app_eq_appLE, â† ConcreteCategory.comp_apply, - CommRingCat.hom_comp,
      Scheme.Hom.appLE_comp_appLE, â† Functor.map_comp, hâ‚]
  Â· simp

include hc in
lemma exists_app_map_eq_zero_of_isLimit [âˆ€ {i j} (f : i âŸ¶ j), IsAffineHom (D.map f)]
    {i : I} {U : (D.obj i).Opens} (hU : IsCompact (X := D.obj i) U) (s : Î“(D.obj i, U))
    (hs : (c.Ï€.app i).app U s = 0) :
    âˆƒ (j : I) (f : j âŸ¶ i), (D.map f).app U s = 0 := by
  have : CompactSpace â†¥((opensDiagram D i U).obj (Over.mk (ğŸ™ i))) :=
    isCompact_iff_compactSpace.mp (by simpa)
  have H : (D.map (ğŸ™ _) â»Â¹áµ U).Î¹ ''áµ âŠ¤ â‰¤ U := by simp
  obtain âŸ¨j, f, hfâŸ© := exists_appTop_map_eq_zero_of_isLimit _ _
    (isLimitOpensCone D c hc i U) (i := .mk (ğŸ™ i))
    ((D.obj i).presheaf.map (homOfLE (show (D.map (ğŸ™ _) â»Â¹áµ U).Î¹ ''áµ âŠ¤ â‰¤ U by simp)).op s) (by
      rw [â† map_zero (c.pt.presheaf.map (homOfLE
        (show (c.Ï€.app i â»Â¹áµ U).Î¹ ''áµ âŠ¤ â‰¤ c.Ï€.app i â»Â¹áµ U by simp)).op).hom, â† hs]
      dsimp [Scheme.Opens.toScheme_presheaf_obj]
      rw [â† ConcreteCategory.comp_apply, â† ConcreteCategory.comp_apply]
      congr! 2
      simp [Scheme.Hom.app_eq_appLE, Scheme.Hom.resLE_appLE])
  dsimp at hf
  refine âŸ¨j.left, f.left, ?_âŸ©
  have hf' : f.left = j.hom := by simpa using Over.w f
  convert congr((D.obj j.left).presheaf.map (homOfLE
    (show D.map f.left â»Â¹áµ U â‰¤ (D.map j.hom â»Â¹áµ U).Î¹ ''áµ âŠ¤ by simp [hf'])).op $hf)
  Â· dsimp [Scheme.Opens.toScheme_presheaf_obj]
    rw [â† ConcreteCategory.comp_apply, â† ConcreteCategory.comp_apply]
    congr! 2
    simp [Scheme.Hom.app_eq_appLE, Scheme.Hom.resLE_appLE]
  Â· simp

include hc in
lemma exists_app_map_eq_map_of_isLimit [âˆ€ {i j} (f : i âŸ¶ j), IsAffineHom (D.map f)]
    {i : I} {U : (D.obj i).Opens} (hU : IsCompact (X := D.obj i) U) (s t : Î“(D.obj i, U))
    (hs : (c.Ï€.app i).app U s = (c.Ï€.app i).app U t) :
    âˆƒ (j : I) (f : j âŸ¶ i), (D.map f).app U s = (D.map f).app U t := by
  simpa [sub_eq_zero] using exists_app_map_eq_zero_of_isLimit _ _ hc hU (s - t)
    (by simpa [map_sub, sub_eq_zero])

include hc in
lemma exists_appTop_Ï€_eq_of_isAffine_of_isLimit
    [âˆ€ i, IsAffine (D.obj i)] (s : Î“(c.pt, âŠ¤)) :
    âˆƒ (i : I) (t : Î“(D.obj i, âŠ¤)), (c.Ï€.app i).appTop t = s := by
  have : âˆ€ i, IsAffine (D.op.obj i).unop := by dsimp; infer_instance
  exact âŸ¨_, (Types.jointly_surjective_of_isColimit
    (isColimitOfPreserves (Scheme.Î“ â‹™ forget _) hc.op) s).choose_specâŸ©

include hc in
lemma exists_appTop_Ï€_eq_of_isLimit [âˆ€ {i j} (f : i âŸ¶ j), IsAffineHom (D.map f)]
    (s : Î“(c.pt, âŠ¤)) [âˆ€ i, CompactSpace (D.obj i)] [âˆ€ i, QuasiSeparatedSpace (D.obj i)] :
    âˆƒ (i : I) (t : Î“(D.obj i, âŠ¤)), s = (c.Ï€.app i).appTop t := by
  classical
  have := Scheme.compactSpace_of_isLimit _ _ hc
  have (x : c.pt) : âˆƒ (i : I) (U : (D.obj i).Opens) (hU : IsAffineOpen U)
      (hU : (c.Ï€.app i).base x âˆˆ U) (t : Î“(D.obj i, U)), (c.Ï€.app i).app U t = s |_ _ := by
    have i := IsCofiltered.nonempty (C := I).some
    obtain âŸ¨_, âŸ¨U, hU : IsAffineOpen U, rflâŸ©, hxU, -âŸ© :=
      (D.obj i).isBasis_affineOpens.exists_subset_of_mem_open
        (Set.mem_univ ((c.Ï€.app i).base x)) isOpen_univ
    have (j : Over i) : IsAffine ((opensDiagram D i U).obj j) := hU.preimage (D.map _)
    obtain âŸ¨j, t, hjâŸ© := exists_appTop_Ï€_eq_of_isAffine_of_isLimit _ _
      (isLimitOpensCone D c hc i U) (s |_ _)
    refine âŸ¨j.left, (D.map j.hom â»Â¹áµ U).Î¹ ''áµ âŠ¤, by simpa using hU.preimage (D.map _), ?_, t, ?_âŸ©
    Â· suffices (c.Ï€.app j.1 â‰« D.map j.hom).base x âˆˆ U by simpa [-Cone.w] using this
      rwa [Cone.w]
    Â· have H : c.Ï€.app j.left â»Â¹áµ (D.map j.hom â»Â¹áµ U).Î¹ ''áµ âŠ¤ â‰¤ (c.Ï€.app i â»Â¹áµ U).Î¹ ''áµ âŠ¤ := by
        simp [â† Scheme.Hom.comp_preimage]
      convert congr(c.pt.presheaf.map (homOfLE H).op $hj)
      Â· convert ConcreteCategory.comp_apply _ _ _
        congr
        simp [Scheme.Hom.app_eq_appLE, Scheme.Hom.resLE_appLE]
      Â· dsimp [TopCat.Presheaf.restrictOpen, TopCat.Presheaf.restrict]
        change _ = (c.pt.presheaf.map (homOfLE _).op â‰« c.pt.presheaf.map (homOfLE _).op) s
        rw [â† Functor.map_comp]
        rfl
  choose i U hU hxU t ht using this
  dsimp at ht
  have (x y : c.pt) : âˆƒ (j : I) (fjx : j âŸ¶ i x) (fjy : j âŸ¶ i y),
      (D.map fjx).app (U x) (t x) |_ (D.map fjx â»Â¹áµ U x âŠ“ D.map fjy â»Â¹áµ U y) =
      (D.map fjy).app (U y) (t y) |_ (D.map fjx â»Â¹áµ U x âŠ“ D.map fjy â»Â¹áµ U y) := by
    obtain âŸ¨j, fjx, fjy, -âŸ© := IsCofilteredOrEmpty.cone_objs (i x) (i y)
    obtain âŸ¨k, fkj, hkâŸ© := exists_app_map_eq_zero_of_isLimit D c hc
      (((hU x).preimage (D.map fjx)).isCompact.inter_of_isOpen
        ((hU y).preimage (D.map fjy)).isCompact ((U x).2.preimage (D.map fjx).continuous)
        ((U y).2.preimage (D.map fjy).continuous))
      ((D.map fjx).app (U x) (t x) |_ (D.map fjx â»Â¹áµ U x âŠ“ D.map fjy â»Â¹áµ U y) -
        (D.map fjy).app (U y) (t y) |_ (D.map fjx â»Â¹áµ U x âŠ“ D.map fjy â»Â¹áµ U y)) (by
      dsimp [TopCat.Presheaf.restrictOpen, TopCat.Presheaf.restrict]
      simp only [map_sub, sub_eq_zero, â† ConcreteCategory.comp_apply,
        Scheme.Hom.app_eq_appLE, Scheme.Hom.appLE_map, Scheme.Hom.appLE_comp_appLE,
        Cone.w]
      simp_rw [Scheme.Hom.appLE, ConcreteCategory.comp_apply, ht, TopCat.Presheaf.restrictOpen,
        TopCat.Presheaf.restrict, â† ConcreteCategory.comp_apply, â† Functor.map_comp]
      rfl)
    refine âŸ¨k, fkj â‰« fjx, fkj â‰« fjy, ?_âŸ©
    have H : (D.map (fkj â‰« fjx) â»Â¹áµ U x âŠ“ D.map (fkj â‰« fjy) â»Â¹áµ U y) â‰¤
        D.map fkj â»Â¹áµ ((D.map fjx â»Â¹áµ U x âŠ“ D.map fjy â»Â¹áµ U y)) := by simp; rfl
    apply_fun (D.obj k).presheaf.map (homOfLE H).op at hk
    dsimp [TopCat.Presheaf.restrictOpen, TopCat.Presheaf.restrict] at hk âŠ¢
    simpa [sub_eq_zero, â† ConcreteCategory.comp_apply, - Scheme.Hom.comp_appLE,
      Scheme.Hom.app_eq_appLE, Scheme.Hom.appLE_comp_appLE] using hk
  choose j fjx fjy hj using this
  obtain âŸ¨Ïƒ, hÏƒâŸ© := CompactSpace.elim_nhds_subcover (fun x â†¦ ((c.Ï€.app (i x)) â»Â¹áµ U x).1)
    (fun x â†¦ ((c.Ï€.app (i x)) â»Â¹áµ U x).2.mem_nhds (by exact hxU x))
  choose Ïƒi hÏƒiÏƒ hÏƒi using fun x â†¦ Set.mem_iUnionâ‚‚.mp (hÏƒ.ge (Set.mem_univ x))
  let S : Finset _ := Ïƒ.image i âˆª Finset.imageâ‚‚ j Ïƒ Ïƒ
  have hiS {x} (hx : x âˆˆ Ïƒ) : i x âˆˆ S := Finset.subset_union_left (Finset.mem_image_of_mem i hx)
  have hjS {x y} (hx : x âˆˆ Ïƒ) (hy : y âˆˆ Ïƒ) : j x y âˆˆ S :=
    Finset.subset_union_right (Finset.mem_imageâ‚‚_of_mem hx hy)
  obtain âŸ¨k, fk, hkâŸ© := IsCofiltered.inf_exists S
    (Ïƒ.attach.imageâ‚‚ (fun (x y : Ïƒ) â†¦ âŸ¨j x.1 y.1, i x.1, hjS x.2 y.2, hiS x.2, fjx x yâŸ©) Ïƒ.attach âˆª
    Ïƒ.attach.imageâ‚‚ (fun (x y : Ïƒ) â†¦ âŸ¨j x.1 y.1, i y.1, hjS x.2 y.2, hiS y.2, fjy x yâŸ©) Ïƒ.attach)
  have hkâ‚ {x y} (hx : x âˆˆ Ïƒ) (hy : y âˆˆ Ïƒ) := hk (hjS hx hy) (hiS hx) (f := fjx x y)
    (Finset.subset_union_left (Finset.mem_imageâ‚‚.mpr âŸ¨âŸ¨x, hxâŸ©, by simp, âŸ¨y, hyâŸ©, by simp, rflâŸ©))
  have hkâ‚‚ {x y} (hx : x âˆˆ Ïƒ) (hy : y âˆˆ Ïƒ) := hk (hjS hx hy) (hiS hy) (f := fjy x y)
    (Finset.subset_union_right (Finset.mem_imageâ‚‚.mpr âŸ¨âŸ¨x, hxâŸ©, by simp, âŸ¨y, hyâŸ©, by simp, rflâŸ©))
  obtain âŸ¨k', fk'k, hk'âŸ© := exists_map_eq_top D c hc
    (â¨† (x : _) (hx : x âˆˆ Ïƒ), D.map (fk (hiS hx)) â»Â¹áµ U x) (by
    apply SetLike.coe_injective
    simpa [â† Set.preimage_comp, â† TopCat.coe_comp, â† Scheme.Hom.comp_base])
  have := ((Presheaf.isSheaf_iff_isSheaf_forget _ _ (forget _)).mp (D.obj k').IsSheaf).isSheafFor
    (.ofArrows (fun x : Ïƒ â†¦ D.map (fk'k â‰« fk (hiS x.2)) â»Â¹áµ U x.1) fun x â†¦ homOfLE le_top)
    (fun x _ â†¦ by
      obtain âŸ¨ix, hix, hâŸ© : âˆƒ ix, âˆƒ (h : ix âˆˆ Ïƒ), (D.map (fk'k â‰« fk (hiS h))).base x âˆˆ U ix := by
        simpa using hk'.ge (Set.mem_univ x)
      refine âŸ¨D.map (fk'k â‰« fk (hiS hix)) â»Â¹áµ U ix, homOfLE le_top,
        Sieve.ofArrows_mk (I := Ïƒ) _ _ âŸ¨ix, hixâŸ©, hâŸ©)
  rw [â† Presieve.isSheafFor_iff_generate, Presieve.isSheafFor_arrows_iff] at this
  obtain âŸ¨tâ‚€, htâ‚€, -âŸ© := this (fun x â†¦ (D.map _).app _ (t x)) fun x y V fVx fVy _ â†¦ by
    have H : V â‰¤ D.map (fk'k â‰« fk (hjS x.2 y.2)) â»Â¹áµ
        (D.map (fjx â†‘x â†‘y) â»Â¹áµ U â†‘x âŠ“ D.map (fjy â†‘x â†‘y) â»Â¹áµ U â†‘y) := by
      change V â‰¤ (D.map (fk'k â‰« fk (hjS x.2 y.2)) â‰« D.map (fjx â†‘x â†‘y)) â»Â¹áµ U x âŠ“
        (D.map (fk'k â‰« fk (hjS x.2 y.2)) â‰« D.map (fjy x y)) â»Â¹áµ U y
      rw [â† Functor.map_comp, â† Functor.map_comp, Category.assoc, Category.assoc,
        hkâ‚ x.2 y.2, hkâ‚‚ x.2 y.2, le_inf_iff]
      exact âŸ¨fVx.le, fVy.leâŸ©
    convert congr(((D.map (fk'k â‰« fk (hjS x.2 y.2))).app _ â‰«
      (D.obj k').presheaf.map (homOfLE H).op) $(hj x y)) using 1
    Â· dsimp [TopCat.Presheaf.restrictOpen, TopCat.Presheaf.restrict]
      simp only [â† ConcreteCategory.comp_apply]
      congr 2
      simp [Scheme.Hom.app_eq_appLE, Scheme.Hom.appLE_comp_appLE,
        - Scheme.Hom.comp_appLE, â† Functor.map_comp, hkâ‚]
    Â· dsimp [TopCat.Presheaf.restrictOpen, TopCat.Presheaf.restrict]
      simp only [â† ConcreteCategory.comp_apply]
      congr 2
      simp [Scheme.Hom.app_eq_appLE, Scheme.Hom.appLE_comp_appLE,
        - Scheme.Hom.comp_appLE, â† Functor.map_comp, hkâ‚‚]
  refine âŸ¨k', tâ‚€, TopCat.Presheaf.section_ext c.pt.sheaf _ _ _ fun y hy â†¦ c.pt.presheaf.germ_ext
    (c.Ï€.app _ â»Â¹áµ U (Ïƒi y)) (hÏƒi y) (homOfLE le_top) (homOfLE le_top) ?_âŸ©
  have H : c.Ï€.app (i (Ïƒi y)) â»Â¹áµ U (Ïƒi y) â‰¤
      c.Ï€.app k' â»Â¹áµ D.map (fk'k â‰« fk (hiS (hÏƒiÏƒ _))) â»Â¹áµ U (Ïƒi y) := by
    rw [â† Scheme.Hom.comp_preimage, Cone.w]
  convert congr(c.pt.presheaf.map (homOfLE H).op ((c.Ï€.app k').app _ $(htâ‚€ âŸ¨_, hÏƒiÏƒ yâŸ©))).symm
  Â· refine (ht (Ïƒi y)).symm.trans ?_
    dsimp [Scheme.Opens.toScheme_presheaf_obj]
    rw [â† ConcreteCategory.comp_apply, â† ConcreteCategory.comp_apply]
    congr 2
    simp [Scheme.Hom.app_eq_appLE, Scheme.Hom.appLE_comp_appLE, -Scheme.Hom.comp_appLE]
  Â· dsimp [Scheme.Opens.toScheme_presheaf_obj]
    rw [â† ConcreteCategory.comp_apply, â† ConcreteCategory.comp_apply,
      â† ConcreteCategory.comp_apply]
    congr 2
    simp [Scheme.Hom.app_eq_appLE]

include hc in
lemma nonempty_isColimit_Î“_mapCocone [âˆ€ {i j} (f : i âŸ¶ j), IsAffineHom (D.map f)]
    [âˆ€ i, CompactSpace (D.obj i)] [âˆ€ i, QuasiSeparatedSpace (D.obj i)] :
    Nonempty (IsColimit (Scheme.Î“.mapCocone c.op)) := by
  have : ReflectsFilteredColimits (forget CommRingCat) :=
    âŸ¨fun _ â†¦ reflectsColimitsOfShape_of_reflectsIsomorphismsâŸ©
  refine ReflectsColimit.reflects (F := forget _) (Types.FilteredColimit.isColimitOf' _ _ ?_ ?_)
  Â· exact fun s â†¦ âŸ¨.op _, (exists_appTop_Ï€_eq_of_isLimit D c hc s).choose_specâŸ©
  Â· exact fun i s t e â†¦ âŸ¨_, Quiver.Hom.op _,
      (exists_app_map_eq_map_of_isLimit _ _ hc isCompact_univ s t e).choose_spec.choose_specâŸ©

instance [âˆ€ {i j} (f : i âŸ¶ j), IsAffineHom (D.map f)]
    [âˆ€ i, CompactSpace (D.obj i)] [âˆ€ i, QuasiSeparatedSpace (D.obj i)] :
    PreservesLimit D Scheme.Î“.rightOp :=
  have : PreservesColimit D.op Scheme.Î“ := âŸ¨fun hc â†¦ nonempty_isColimit_Î“_mapCocone D _ hc.unopâŸ©
  preservesLimit_rightOp _ _

end sections

end AlgebraicGeometry
