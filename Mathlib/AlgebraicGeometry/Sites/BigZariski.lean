/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou, Adam Topaz
-/
import Mathlib.AlgebraicGeometry.Pullbacks
import Mathlib.CategoryTheory.Sites.Pretopology
import Mathlib.CategoryTheory.Sites.Canonical
/-!
# The big Zariski site of schemes

In this file, we define the Zariski topology, as a Grothendieck topology on the
category `Scheme.{u}`: this is `Scheme.zariskiTopology.{u}`. If `X : Scheme.{u}`,
the Zariski topology on `Over X` can be obtained as `Scheme.zariskiTopology.over X`
(see `CategoryTheory.Sites.Over`.).

TODO:
* If `Y : Scheme.{u}`, define a continuous functor from the category of opens of `Y`
to `Over Y`, and show that a presheaf on `Over Y` is a sheaf for the Zariski topology
iff its "restriction" to the topological space `Z` is a sheaf for all `Z : Over Y`.
* We should have good notions of (pre)sheaves of `Type (u + 1)` (e.g. associated
sheaf functor, pushforward, pullbacks) on `Scheme.{u}` for this topology. However,
some constructions in the `CategoryTheory.Sites` folder currently assume that
the site is a small category: this should be generalized. As a result,
this big Zariski site can considered as a test case of the Grothendieck topology API
for future applications to Ã©tale cohomology.

-/

universe v u

open CategoryTheory

namespace AlgebraicGeometry

namespace Scheme

/-- The Zariski pretopology on the category of schemes. -/
def zariskiPretopology : Pretopology (Scheme.{u}) where
  coverings Y S := âˆƒ (U : OpenCover.{u} Y), S = Presieve.ofArrows U.obj U.map
  has_isos Y X f _ := âŸ¨openCoverOfIsIso f, (Presieve.ofArrows_pUnit _).symmâŸ©
  pullbacks := by
    rintro Y X f _ âŸ¨U, rflâŸ©
    exact âŸ¨U.pullbackCover' f, (Presieve.ofArrows_pullback _ _ _).symmâŸ©
  transitive := by
    rintro X _ T âŸ¨U, rflâŸ© H
    choose V hV using H
    use U.bind (fun j => V (U.map j) âŸ¨jâŸ©)
    simpa only [OpenCover.bind, â† hV] using Presieve.ofArrows_bind U.obj U.map _
      (fun _ f H => (V f H).obj) (fun _ f H => (V f H).map)

/-- The Zariski topology on the category of schemes. -/
abbrev zariskiTopology : GrothendieckTopology (Scheme.{u}) :=
  zariskiPretopology.toGrothendieck

lemma zariskiPretopology_openCover {Y : Scheme.{u}} (U : OpenCover.{u} Y) :
    zariskiPretopology Y (Presieve.ofArrows U.obj U.map) :=
  âŸ¨U, rflâŸ©

lemma zariskiTopology_openCover {Y : Scheme.{u}} (U : OpenCover.{v} Y) :
    zariskiTopology Y (Sieve.generate (Presieve.ofArrows U.obj U.map)) := by
  let V : OpenCover.{u} Y :=
    { J := Y
      obj := fun y => U.obj (U.f y)
      map := fun y => U.map (U.f y)
      f := id
      Covers := U.Covers
      IsOpen := fun _ => U.IsOpen _ }
  refine âŸ¨_, zariskiPretopology_openCover V, ?_âŸ©
  rintro _ _ âŸ¨yâŸ©
  exact âŸ¨_, ğŸ™ _, U.map (U.f y), âŸ¨_âŸ©, by simpâŸ©

lemma subcanonical_zariskiTopology : Sheaf.Subcanonical zariskiTopology := by
  apply Sheaf.Subcanonical.of_yoneda_isSheaf
  intro X
  rw [Presieve.isSheaf_pretopology]
  rintro Y S âŸ¨ğ“¤,rflâŸ© x hx
  let e : Y âŸ¶ X := ğ“¤.glueMorphisms (fun j => x (ğ“¤.map _) (.mk _)) <| by
    intro i j
    apply hx
    exact Limits.pullback.condition
  refine âŸ¨e, ?_, ?_âŸ©
  Â· rintro Z e âŸ¨jâŸ©
    dsimp [e]
    rw [ğ“¤.Î¹_glueMorphisms]
  Â· intro e' h
    apply ğ“¤.hom_ext
    intro j
    rw [ğ“¤.Î¹_glueMorphisms]
    exact h (ğ“¤.map j) (.mk j)

end Scheme

end AlgebraicGeometry
