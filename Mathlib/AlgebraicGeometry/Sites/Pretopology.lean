/-
Copyright (c) 2026 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.AlgebraicGeometry.Sites.MorphismProperty
import Mathlib.AlgebraicGeometry.PullbackCarrier

/-!
# Grothendieck topology defined by a morphism property

Given a multiplicative morphism property `P` that is stable under base change, we define the
associated (pre)topology on the category of schemes, where coverings are given
by jointly surjective families of morphisms satisfying `P`.

## Implementation details

The pretopology is obtained from the precoverage `AlgebraicGeometry.Scheme.precoverage` defined in
`Mathlib.AlgebraicGeometry.Sites.MorphismProperty`. The definition is postponed to this file,
because the former does not have `HasPullbacks Scheme`.
-/

universe v u

open CategoryTheory Limits

namespace AlgebraicGeometry.Scheme

/--
The pretopology on the category of schemes defined by covering families where the components
satisfy `P`.
-/
def pretopology (P : MorphismProperty Scheme.{u}) [P.IsStableUnderBaseChange]
    [P.IsMultiplicative] : Pretopology Scheme.{u} :=
  (precoverage P).toPretopology

/-- The Grothendieck topology on the category of schemes induced by the pretopology defined by
`P`-covers. -/
abbrev grothendieckTopology (P : MorphismProperty Scheme.{u}) [P.IsStableUnderBaseChange]
    [P.IsMultiplicative] :
    GrothendieckTopology Scheme.{u} :=
  (pretopology P).toGrothendieck

instance : jointlySurjectivePrecoverage.IsStableUnderBaseChange where
  mem_coverings_of_isPullback {Î¹} S X f hf Y g T pâ‚ pâ‚‚ H x := by
    obtain âŸ¨-, -, âŸ¨iâŸ©, y, hyâŸ© := hf (g.base x)
    clear Y
    have := (H i).hasPullback
    obtain âŸ¨w, hwâŸ© := IsJointlySurjectivePreserving.exists_preimage_fst_triplet_of_prop (P := âŠ¤)
      trivial (f := g) x y hy.symm
    use T i, pâ‚ i, âŸ¨iâŸ©, (H i).isoPullback.inv.base w
    simpa [â† Scheme.comp_base_apply]

/-- The pretopology on the category of schemes defined by jointly surjective families. -/
def jointlySurjectivePretopology : Pretopology Scheme.{u} :=
  jointlySurjectivePrecoverage.toPretopology

variable {P : MorphismProperty Scheme.{u}} [P.IsStableUnderBaseChange] [P.IsMultiplicative]

lemma mem_iff_exists_cover {K : Precoverage Scheme.{u}} {X : Scheme.{u}} {R : Presieve X} :
    R âˆˆ K X â†” âˆƒ (ğ’° : Cover.{u + 1} K X), R = Presieve.ofArrows ğ’°.X ğ’°.f :=
  K.mem_iff_exists_zeroHypercover

lemma mem_pretopology {X : Scheme.{u}} {R : Presieve X} :
    R âˆˆ pretopology P X â†” âˆƒ (ğ’° : Cover.{u + 1} (precoverage P) X), R = Presieve.ofArrows ğ’°.X ğ’°.f :=
  (precoverage P).mem_iff_exists_zeroHypercover

alias âŸ¨exists_cover_of_mem_pretopology, _âŸ© := mem_pretopology

lemma mem_grothendieckTopology {X : Scheme.{u}} {S : Sieve X} :
    S âˆˆ grothendieckTopology P X â†”
      âˆƒ (ğ’° : Cover.{u} (precoverage P) X), Presieve.ofArrows ğ’°.X ğ’°.f â‰¤ S := by
  simp_rw [grothendieckTopology, Pretopology.mem_toGrothendieck]
  refine âŸ¨fun âŸ¨R, hR, hleâŸ© â†¦ ?_, fun âŸ¨ğ’°, hleâŸ© â†¦ âŸ¨.ofArrows ğ’°.X ğ’°.f, ğ’°.memâ‚€, hleâŸ©âŸ©
  rw [mem_pretopology] at hR
  obtain âŸ¨ğ’°, rflâŸ© := hR
  use ğ’°.ulift, le_trans (fun Y g âŸ¨iâŸ© â†¦ .mk _) hle

alias âŸ¨exists_cover_of_mem_grothendieckTopology, _âŸ© := mem_grothendieckTopology

@[grind â†]
lemma Cover.mem_pretopology {X : Scheme.{u}} {ğ’° : X.Cover (precoverage P)} :
    Presieve.ofArrows ğ’°.X ğ’°.f âˆˆ pretopology P X := ğ’°.memâ‚€

@[grind â†]
lemma Cover.mem_grothendieckTopology {X : Scheme.{u}} {ğ’° : X.Cover (precoverage P)} :
    Sieve.ofArrows ğ’°.X ğ’°.f âˆˆ grothendieckTopology P X := by
  rw [Pretopology.mem_toGrothendieck]
  use Presieve.ofArrows ğ’°.X ğ’°.f, ğ’°.memâ‚€
  exact Sieve.le_generate (Presieve.ofArrows ğ’°.X ğ’°.f)

@[deprecated (since := "2025-08-28")]
alias pretopology_cover := Cover.mem_pretopology

@[deprecated (since := "2025-08-28")]
alias grothendieckTopology_cover := Cover.mem_grothendieckTopology

section

@[deprecated (since := "2025-08-18")] alias surjectiveFamiliesPretopology :=
  jointlySurjectivePretopology

/-- The jointly surjective topology on `Scheme` is defined by the same condition as the jointly
surjective pretopology. -/
def jointlySurjectiveTopology : GrothendieckTopology Scheme.{u} :=
  jointlySurjectivePretopology.toGrothendieck.copy (fun X s â†¦ jointlySurjectivePretopology X â†‘s) <|
    funext fun _ â†¦ Set.ext fun s â†¦
      âŸ¨fun âŸ¨_, hp, hpsâŸ© x â†¦ let âŸ¨Y, u, hu, hmemâŸ© := hp x; âŸ¨Y, u, hps _ hu, hmemâŸ©,
      fun hs â†¦ âŸ¨s, hs, le_rflâŸ©âŸ©

theorem mem_jointlySurjectiveTopology_iff_jointlySurjectivePretopology
    {X : Scheme.{u}} {s : Sieve X} :
    s âˆˆ jointlySurjectiveTopology X â†” jointlySurjectivePretopology X â†‘s :=
  Iff.rfl

lemma jointlySurjectiveTopology_eq_toGrothendieck_jointlySurjectivePretopology :
    jointlySurjectiveTopology.{u} = jointlySurjectivePretopology.toGrothendieck :=
  GrothendieckTopology.copy_eq

variable (P)

/--
The pretopology defined by `P`-covers agrees with the
the intersection of the pretopology of surjective families with the pretopology defined by `P`.
-/
lemma pretopology_eq_inf : pretopology P = jointlySurjectivePretopology âŠ“ P.pretopology := by
  ext : 1
  rw [pretopology, Precoverage.toPretopology_toPrecoverage]
  rfl

@[deprecated (since := "2025-08-28")]
alias pretopology_le_inf := pretopology_eq_inf

/--
The Grothendieck topology defined by `P`-covers agrees with the Grothendieck
topology induced by the intersection of the pretopology of surjective families with
the pretopology defined by `P`.
-/
lemma grothendieckTopology_eq_inf :
    grothendieckTopology P = (jointlySurjectivePretopology âŠ“ P.pretopology).toGrothendieck := by
  rw [grothendieckTopology, pretopology_eq_inf]

end

section

variable {Q : MorphismProperty Scheme.{u}} [Q.IsMultiplicative] [Q.IsStableUnderBaseChange]

lemma pretopology_le_pretopology (hPQ : P â‰¤ Q) :
    pretopology P â‰¤ pretopology Q := by
  rintro X R hR
  obtain âŸ¨ğ’°, rflâŸ© := exists_cover_of_mem_pretopology hR
  rw [mem_pretopology]
  use ğ’°.changeProp (fun j â†¦ hPQ _ (ğ’°.map_prop j))
  rfl

lemma grothendieckTopology_le_grothendieckTopology (hPQ : P â‰¤ Q) :
    grothendieckTopology P â‰¤ grothendieckTopology Q :=
  (Pretopology.gi Scheme.{u}).gc.monotone_l (pretopology_le_pretopology hPQ)

end

end AlgebraicGeometry.Scheme
