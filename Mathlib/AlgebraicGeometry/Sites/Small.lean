/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.AlgebraicGeometry.Cover.Over
import Mathlib.AlgebraicGeometry.Sites.Pretopology
import Mathlib.CategoryTheory.Sites.DenseSubsite.InducedTopology
import Mathlib.CategoryTheory.Sites.Over

/-!
# Small sites

In this file we define the small sites associated to morphism properties and give
generating pretopologies.

## Main definitions

- `AlgebraicGeometry.Scheme.overGrothendieckTopology`: the Grothendieck topology on `Over S`
  obtained by localizing the topology on `Scheme` induced by `P` at `S`.
- `AlgebraicGeometry.Scheme.overPretopology`: the pretopology on `Over S` defined by
  `P`-coverings of `S`-schemes. The induced topology agrees with
  `AlgebraicGeometry.Scheme.overGrothendieckTopology`.
- `AlgebraicGeometry.Scheme.smallGrothendieckTopology`: the by the inclusion
  `P.Over âŠ¤ S â¥¤ Over S` induced topology on `P.Over âŠ¤ S`.
- `AlgebraicGeometry.Scheme.smallPretopology`: the pretopology on `P.Over âŠ¤ S` defined by
  `P`-coverings of `S`-schemes with `P`. The induced topology agrees
  with `AlgebraicGeometry.Scheme.smallGrothendieckTopology`.

-/

universe v u

open CategoryTheory Limits

namespace AlgebraicGeometry.Scheme

variable {P Q : MorphismProperty Scheme.{u}} {S : Scheme.{u}}
  [P.IsStableUnderBaseChange]

/-- The presieve defined by a `P`-cover of `S`-schemes. -/
def Cover.toPresieveOver {X : Over S} (ğ’° : Cover.{u} (precoverage P) X.left) [ğ’°.Over S] :
    Presieve X :=
  Presieve.ofArrows (fun i â†¦ (ğ’°.X i).asOver S) (fun i â†¦ (ğ’°.f i).asOver S)

/-- The presieve defined by a `P`-cover of `S`-schemes with `Q`. -/
def Cover.toPresieveOverProp {X : Q.Over âŠ¤ S} (ğ’° : Cover.{u} (precoverage P) X.left) [ğ’°.Over S]
    (h : âˆ€ j, Q (ğ’°.X j â†˜ S)) : Presieve X :=
  Presieve.ofArrows (fun i â†¦ (ğ’°.X i).asOverProp S (h i)) (fun i â†¦ (ğ’°.f i).asOverProp S)

lemma Cover.overEquiv_generate_toPresieveOver_eq_ofArrows {X : Over S}
    (ğ’° : Cover.{u} (precoverage P) X.left)
    [ğ’°.Over S] : Sieve.overEquiv X (Sieve.generate ğ’°.toPresieveOver) =
      Sieve.ofArrows ğ’°.X ğ’°.f := by
  ext V f
  simp only [Sieve.overEquiv_iff, Functor.const_obj_obj, Sieve.generate_apply]
  constructor
  Â· rintro âŸ¨U, h, g, âŸ¨kâŸ©, hcompâŸ©
    exact âŸ¨ğ’°.X k, h.left, ğ’°.f k, âŸ¨kâŸ©, congrArg CommaMorphism.left hcompâŸ©
  Â· rintro âŸ¨U, h, g, âŸ¨kâŸ©, hcompâŸ©
    have : ğ’°.f k â‰« X.hom = ğ’°.X k â†˜ S := comp_over (ğ’°.f k) S
    refine âŸ¨(ğ’°.X k).asOver S, Over.homMk h (by simp [â† hcomp, this]), (ğ’°.f k).asOver S, âŸ¨kâŸ©, ?_âŸ©
    ext : 1
    simpa

lemma Cover.toPresieveOver_le_arrows_iff {X : Over S} (R : Sieve X)
    (ğ’° : Cover.{u} (precoverage P) X.left) [ğ’°.Over S] :
    ğ’°.toPresieveOver â‰¤ R.arrows â†”
      Presieve.ofArrows ğ’°.X ğ’°.f â‰¤ (Sieve.overEquiv X R).arrows := by
  simp_rw [â† Sieve.giGenerate.gc.le_iff_le, â† Sieve.overEquiv_le_overEquiv_iff]
  rw [overEquiv_generate_toPresieveOver_eq_ofArrows]

variable [P.IsMultiplicative] [P.RespectsIso]

variable (P Q S)

/-- The pretopology on `Over S` induced by `P` where coverings are given by `P`-covers
of `S`-schemes. -/
def overPretopology : Pretopology (Over S) where
  coverings Y R := âˆƒ (ğ’° : Cover.{u} (precoverage P) Y.left) (_ : ğ’°.Over S), R = ğ’°.toPresieveOver
  has_isos {X Y} f _ := âŸ¨coverOfIsIso f.left, inferInstance, (Presieve.ofArrows_pUnit _).symmâŸ©
  pullbacks := by
    rintro Y X f _ âŸ¨ğ’°, h, rflâŸ©
    refine âŸ¨ğ’°.pullbackCoverOver' S f.left, inferInstance, ?_âŸ©
    simpa [Cover.toPresieveOver] using
      (Presieve.ofArrows_pullback f (fun i â†¦ (ğ’°.X i).asOver S) (fun i â†¦ (ğ’°.f i).asOver S)).symm
  transitive := by
    rintro X _ T âŸ¨ğ’°, h, rflâŸ© H
    choose V h hV using H
    refine âŸ¨ğ’°.bind (fun j => V ((ğ’°.f j).asOver S) âŸ¨jâŸ©), inferInstance, ?_âŸ©
    convert Presieve.ofArrows_bind _ (fun j â†¦ (ğ’°.f j).asOver S) _
      (fun Y f H j â†¦ ((V f H).X j).asOver S) (fun Y f H j â†¦ ((V f H).f j).asOver S)
    apply hV

/-- The topology on `Over S` induced from the topology on `Scheme` defined by `P`.
This agrees with the topology induced by `S.overPretopology P`, see
`AlgebraicGeometry.Scheme.overGrothendieckTopology_eq_toGrothendieck_overPretopology`. -/
abbrev overGrothendieckTopology : GrothendieckTopology (Over S) :=
  (Scheme.grothendieckTopology P).over S

lemma overGrothendieckTopology_eq_toGrothendieck_overPretopology :
    S.overGrothendieckTopology P = (S.overPretopology P).toGrothendieck := by
  ext X R
  rw [GrothendieckTopology.mem_over_iff]
  constructor
  Â· intro hR
    obtain âŸ¨ğ’°, hleâŸ© := exists_cover_of_mem_grothendieckTopology hR
    rw [mem_grothendieckTopology_iff] at hR
    letI (i : ğ’°.Iâ‚€) : (ğ’°.X i).Over S := { hom := ğ’°.f i â‰« X.hom }
    letI : ğ’°.Over S :=
      { over := inferInstance
        isOver_map := fun i â†¦ âŸ¨rflâŸ© }
    use ğ’°.toPresieveOver, âŸ¨ğ’°, inferInstance, rflâŸ©
    rwa [Cover.toPresieveOver_le_arrows_iff]
  Â· rintro âŸ¨T, âŸ¨ğ’°, h, rflâŸ©, hTâŸ©
    use Presieve.ofArrows ğ’°.X ğ’°.f, ğ’°.mem_pretopology
    rwa [Cover.toPresieveOver_le_arrows_iff] at hT

variable {S}

lemma mem_overGrothendieckTopology (X : Over S) (R : Sieve X) :
    R âˆˆ S.overGrothendieckTopology P X â†”
      âˆƒ (ğ’° : Cover.{u} (precoverage P) X.left) (_ : ğ’°.Over S), ğ’°.toPresieveOver â‰¤ R.arrows := by
  rw [overGrothendieckTopology_eq_toGrothendieck_overPretopology]
  constructor
  Â· rintro âŸ¨T, âŸ¨ğ’°, h, rflâŸ©, hleâŸ©
    use ğ’°, h
  Â· rintro âŸ¨ğ’°, hğ’°, hleâŸ©
    exact âŸ¨ğ’°.toPresieveOver, âŸ¨ğ’°, hğ’°, rflâŸ©, hleâŸ©

variable [Q.IsStableUnderComposition]

variable (S) {P Q} in
lemma locallyCoverDense_of_le (hPQ : P â‰¤ Q) :
    (MorphismProperty.Over.forget Q âŠ¤ S).LocallyCoverDense (overGrothendieckTopology P S) where
  functorPushforward_functorPullback_mem X := by
    intro âŸ¨T, hTâŸ©
    rw [mem_overGrothendieckTopology] at hT âŠ¢
    obtain âŸ¨ğ’°, h, hleâŸ© := hT
    use ğ’°, h
    rintro - - âŸ¨iâŸ©
    have p : Q (ğ’°.X i â†˜ S) := by
      rw [â† comp_over (ğ’°.f i) S]
      exact Q.comp_mem _ _ (hPQ _ <| ğ’°.map_prop i) X.prop
    use (ğ’°.X i).asOverProp S p, MorphismProperty.Over.homMk (ğ’°.f i) (comp_over (ğ’°.f i) S), ğŸ™ _
    exact âŸ¨hle _ âŸ¨iâŸ©, rflâŸ©

instance : (MorphismProperty.Over.forget P âŠ¤ S).LocallyCoverDense (overGrothendieckTopology P S) :=
  locallyCoverDense_of_le S le_rfl

variable (S) {P Q} in
/-- If `P` and `Q` are morphism properties with `P â‰¤ Q`, this is the Grothendieck topology
induced via the forgetful functor `Q.Over âŠ¤ S â¥¤ Over S` by the topology defined by `P`. -/
abbrev smallGrothendieckTopologyOfLE (hPQ : P â‰¤ Q) : GrothendieckTopology (Q.Over âŠ¤ S) :=
  letI : (MorphismProperty.Over.forget Q âŠ¤ S).LocallyCoverDense (overGrothendieckTopology P S) :=
    locallyCoverDense_of_le S hPQ
  (MorphismProperty.Over.forget Q âŠ¤ S).inducedTopology (S.overGrothendieckTopology P)

/-- The Grothendieck topology on the category of schemes over `S` with `P` induced by `P`, i.e.
coverings are simply surjective families. This is the induced topology by the topology on `S`
defined by `P` via the inclusion `P.Over âŠ¤ S â¥¤ Over S`.

This is a special case of `smallGrothendieckTopologyOfLE` for the case `P = Q`. -/
abbrev smallGrothendieckTopology : GrothendieckTopology (P.Over âŠ¤ S) :=
  (MorphismProperty.Over.forget P âŠ¤ S).inducedTopology (S.overGrothendieckTopology P)

variable [Q.IsStableUnderBaseChange] [Q.HasOfPostcompProperty Q]

/-- The pretopology defined on the subcategory of `S`-schemes satisfying `Q` where coverings
are given by `P`-coverings in `S`-schemes satisfying `Q`.
The most common case is `P = Q`. In this case, this is simply surjective families
in `S`-schemes with `P`. -/
def smallPretopology : Pretopology (Q.Over âŠ¤ S) where
  coverings Y R :=
    âˆƒ (ğ’° : Cover.{u} (precoverage P) Y.left) (_ : ğ’°.Over S) (h : âˆ€ j : ğ’°.Iâ‚€, Q (ğ’°.X j â†˜ S)),
      R = ğ’°.toPresieveOverProp h
  has_isos {X Y} f := âŸ¨coverOfIsIso f.left, inferInstance, fun _ â†¦ Y.prop,
    (Presieve.ofArrows_pUnit _).symmâŸ©
  pullbacks := by
    rintro Y X f _ âŸ¨ğ’°, h, p, rflâŸ©
    refine âŸ¨ğ’°.pullbackCoverOverProp' S f.left (Q := Q) Y.prop X.prop p, inferInstance, ?_, ?_âŸ©
    Â· intro j
      apply MorphismProperty.Comma.prop
    Â· exact (Presieve.ofArrows_pullback f (fun i â†¦ âŸ¨(ğ’°.X i).asOver S, p iâŸ©)
        (fun i â†¦ âŸ¨(ğ’°.f i).asOver S, trivial, trivialâŸ©)).symm
  transitive := by
    rintro X _ T âŸ¨ğ’°, h, p, rflâŸ© H
    choose V h pV hV using H
    let ğ’±j (j : ğ’°.Iâ‚€) : (Cover (precoverage P) ((ğ’°.X j).asOverProp S (p j)).left) :=
      V ((ğ’°.f j).asOverProp S) âŸ¨jâŸ©
    refine âŸ¨ğ’°.bind (fun j â†¦ ğ’±j j), inferInstance, fun j â†¦ pV _ _ _, ?_âŸ©
    convert Presieve.ofArrows_bind _ (fun j â†¦ ((ğ’°.f j).asOverProp S)) _
      (fun Y f H j â†¦ ((V f H).X j).asOverProp S (pV _ _ _))
      (fun Y f H j â†¦ ((V f H).f j).asOverProp S)
    apply hV

variable (S) {P Q} in
lemma smallGrothendieckTopologyOfLE_eq_toGrothendieck_smallPretopology (hPQ : P â‰¤ Q) :
    S.smallGrothendieckTopologyOfLE hPQ = (S.smallPretopology P Q).toGrothendieck := by
  ext X R
  simp only [Pretopology.mem_toGrothendieck, Functor.mem_inducedTopology_sieves_iff,
    MorphismProperty.Comma.forget_obj, mem_overGrothendieckTopology]
  constructor
  Â· intro âŸ¨ğ’°, h, leâŸ©
    have hj (j : ğ’°.Iâ‚€) : Q (ğ’°.X j â†˜ S) := by
      rw [â† comp_over (ğ’°.f j)]
      exact Q.comp_mem _ _ (hPQ _ <| ğ’°.map_prop _) X.prop
    refine âŸ¨ğ’°.toPresieveOverProp hj, ?_, ?_âŸ©
    Â· use ğ’°, h, hj
    Â· rintro - - âŸ¨iâŸ©
      let fi : (ğ’°.X i).asOverProp S (hj i) âŸ¶ X := (ğ’°.f i).asOverProp S
      have : R.functorPushforward _ ((MorphismProperty.Over.forget Q âŠ¤ S).map fi) := le _ âŸ¨iâŸ©
      rwa [Sieve.functorPushforward_apply,
        Sieve.mem_functorPushforward_iff_of_full_of_faithful] at this
  Â· rintro âŸ¨T, âŸ¨ğ’°, h, p, rflâŸ©, leâŸ©
    use ğ’°, h
    rintro - - âŸ¨iâŸ©
    exact âŸ¨(ğ’°.X i).asOverProp S (p i), (ğ’°.f i).asOverProp S, ğŸ™ _, le _ âŸ¨iâŸ©, rflâŸ©

lemma smallGrothendieckTopology_eq_toGrothendieck_smallPretopology [P.HasOfPostcompProperty P] :
    S.smallGrothendieckTopology P = (S.smallPretopology P P).toGrothendieck :=
  S.smallGrothendieckTopologyOfLE_eq_toGrothendieck_smallPretopology le_rfl

variable {P Q}

lemma mem_toGrothendieck_smallPretopology (X : Q.Over âŠ¤ S) (R : Sieve X) :
    R âˆˆ (S.smallPretopology P Q).toGrothendieck X â†”
      âˆ€ x : X.left, âˆƒ (Y : Q.Over âŠ¤ S) (f : Y âŸ¶ X) (y : Y.left),
        R f âˆ§ P f.left âˆ§ f.left y = x := by
  rw [Pretopology.mem_toGrothendieck]
  refine âŸ¨?_, fun h â†¦ ?_âŸ©
  Â· rintro âŸ¨T, âŸ¨ğ’°, h, p, rflâŸ©, hleâŸ©
    intro x
    obtain âŸ¨y, hyâŸ© := ğ’°.covers x
    refine âŸ¨(ğ’°.X (ğ’°.idx x)).asOverProp S (p _), (ğ’°.f (ğ’°.idx x)).asOverProp S, y, hle _ ?_,
      ğ’°.map_prop _, hyâŸ©
    use ğ’°.idx x
  Â· choose Y f y hf hP hy using h
    let ğ’° : X.left.Cover (precoverage P) :=
      { Iâ‚€ := X.left,
        X := fun i â†¦ (Y i).left
        f := fun i â†¦ (f i).left
        memâ‚€ := by
          rw [presieveâ‚€_mem_precoverage_iff]
          refine âŸ¨fun x â†¦ âŸ¨x, y x, hy xâŸ©, hPâŸ© }
    letI : ğ’°.Over S :=
      { over := fun i â†¦ inferInstance
        isOver_map := fun i â†¦ inferInstance }
    refine âŸ¨ğ’°.toPresieveOverProp fun i â†¦ MorphismProperty.Comma.prop _, ?_, ?_âŸ©
    Â· use ğ’°, inferInstance, fun i â†¦ MorphismProperty.Comma.prop _
    Â· rintro - - âŸ¨iâŸ©
      exact hf i

lemma mem_smallGrothendieckTopology [P.HasOfPostcompProperty P] (X : P.Over âŠ¤ S) (R : Sieve X) :
    R âˆˆ S.smallGrothendieckTopology P X â†”
      âˆƒ (ğ’° : Cover.{u} (precoverage P) X.left) (_ : ğ’°.Over S) (h : âˆ€ j, P (ğ’°.X j â†˜ S)),
          ğ’°.toPresieveOverProp h â‰¤ R.arrows := by
  rw [smallGrothendieckTopology_eq_toGrothendieck_smallPretopology]
  constructor
  Â· rintro âŸ¨T, âŸ¨ğ’°, h, p, rflâŸ©, hleâŸ©
    use ğ’°, h, p
  Â· rintro âŸ¨ğ’°, hğ’°, p, hleâŸ©
    exact âŸ¨ğ’°.toPresieveOverProp p, âŸ¨ğ’°, hğ’°, p, rflâŸ©, hleâŸ©

end AlgebraicGeometry.Scheme
