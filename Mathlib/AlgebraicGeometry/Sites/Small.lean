/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.AlgebraicGeometry.Pullbacks
import Mathlib.AlgebraicGeometry.Cover.Over
import Mathlib.AlgebraicGeometry.Sites.MorphismProperty
import Mathlib.CategoryTheory.Sites.MorphismProperty
import Mathlib.CategoryTheory.Limits.MorphismProperty
import Mathlib.CategoryTheory.Sites.Over
import Mathlib.CategoryTheory.Sites.InducedTopology

/-!
# Small sites

In this file we define the small sites associated to morphism properties and give
generating pretopologies.

-/

universe v u

open CategoryTheory Limits

namespace AlgebraicGeometry.Scheme

variable (P : MorphismProperty Scheme.{u}) [P.IsMultiplicative] [P.RespectsIso]
  [P.IsStableUnderBaseChange] [IsJointlySurjectivePreserving P] [P.HasOfPostcompProperty]

variable (S : Scheme.{u})

instance {C : Type*} [Category C] {S : C} (X : Over S) : OverClass X.left S where
  hom := X.hom

instance {C : Type*} [Category C] {S : C} {X Y : Over S} (f : X âŸ¶ Y) : HomIsOver f.left S where
  comp_over := Over.w f

variable {S P} in
def Cover.toPresieveOver {X : Over S} (ğ’° : Cover.{u} P X.left) [ğ’°.Over S] : Presieve X :=
  Presieve.ofArrows (fun i â†¦ (ğ’°.obj i).asOver S) (fun i â†¦ (ğ’°.map i).asOver S)

variable {S} {P} {Q : MorphismProperty Scheme.{u}} in
def Cover.toPresieveOverWithProp {X : P.Over âŠ¤ S} (ğ’° : Cover.{u} Q X.left) [ğ’°.Over S]
    (h : âˆ€ j, P (ğ’°.obj j â†˜ S)) :
    Presieve X :=
  Presieve.ofArrows (fun i â†¦ âŸ¨(ğ’°.obj i).asOver S, h iâŸ©)
    (fun i â†¦ âŸ¨(ğ’°.map i).asOver S, trivial, trivialâŸ©)

lemma Cover.toPresieveOver_pullbackCover {X W : Over S} (ğ’° : Cover.{u} P X.left) [ğ’°.Over S]
    (f : W âŸ¶ X) :
    (ğ’°.pullbackCoverOver' S f.left).toPresieveOver = Presieve.pullbackArrows f ğ’°.toPresieveOver := by
  simpa [Cover.toPresieveOver] using
    Presieve.ofArrows_pullback f (fun i â†¦ (ğ’°.obj i).asOver S) (fun i â†¦ (ğ’°.map i).asOver S)

lemma foo {X : Over S} (ğ’° : Cover.{u} P X.left)
    [ğ’°.Over S] :
    Sieve.overEquiv X (Sieve.generate ğ’°.toPresieveOver) =
      Sieve.ofArrows ğ’°.obj ğ’°.map := by
  ext V f
  rw [Sieve.overEquiv_iff]
  simp
  constructor
  Â· rintro âŸ¨U, h, g, âŸ¨kâŸ©, hcompâŸ©
    use ğ’°.obj k, h.left, ğ’°.map k
    constructor
    Â· use k
    Â· apply_fun CommaMorphism.left at hcomp
      exact hcomp
  Â· rintro âŸ¨U, h, g, âŸ¨kâŸ©, hcompâŸ©
    have : ğ’°.map k â‰« X.hom = ğ’°.obj k â†˜ S := comp_over (ğ’°.map k) S
    let h' : Over.mk (f â‰« X.hom) âŸ¶ OverClass.asOver (X := ğ’°.obj k) S :=
      Over.homMk h (by simp [â† hcomp, this])
    use OverClass.asOver (X := ğ’°.obj k) S, h', OverClass.asOverHom S (ğ’°.map k)
    constructor
    Â· use k
    Â· ext : 1
      simpa

lemma Cover.toPresieveOver_le_arrows_iff {X : Over S} (R : Sieve X) (ğ’° : Cover.{u} P X.left)
    [ğ’°.Over S] :
     ğ’°.toPresieveOver â‰¤ R.arrows â†”
      Presieve.ofArrows ğ’°.obj ğ’°.map â‰¤ (Sieve.overEquiv X R).arrows := by
  constructor
  Â· rintro h - - âŸ¨iâŸ©
    simp [Sieve.overEquiv]
    have : ğ’°.toPresieveOver (OverClass.asOverHom S (ğ’°.map i)) := by use i
    have := h (OverClass.asOver (X := (ğ’°.obj i)) S) this
    use OverClass.asOver (X := (ğ’°.obj i)) S, OverClass.asOverHom S (ğ’°.map i), ğŸ™ _
    constructor
    Â· exact this
    Â· rfl
  Â· intro h Y g âŸ¨iâŸ©
    have : Presieve.ofArrows ğ’°.obj ğ’°.map (ğ’°.map i) := by use i
    have := h _ this
    obtain âŸ¨Z, g, h, hg, hcompâŸ© := this
    have : ğ’°.obj i â†˜ S = ğ’°.map i â‰« X.hom := (comp_over (ğ’°.map i) S).symm
    simp at hcomp
    let h' : (ğ’°.obj i).asOver S âŸ¶ Z :=
      Over.homMk h (by simp [this, hcomp])
    have : (ğ’°.map i).asOver S = h' â‰« g := by
      ext : 1
      exact hcomp
    rw [this]
    apply R.downward_closed
    exact hg

def overPretopology : Pretopology (Over S) where
  coverings Y R := âˆƒ (ğ’° : Cover.{u} P Y.left) (_ : ğ’°.Over S), R = ğ’°.toPresieveOver
  has_isos {X Y} f _ := âŸ¨coverOfIsIso f.left, inferInstance, (Presieve.ofArrows_pUnit _).symmâŸ©
  pullbacks := by
    rintro Y X f _ âŸ¨ğ’°, h, rflâŸ©
    refine âŸ¨ğ’°.pullbackCoverOver' S f.left, inferInstance, ?_âŸ©
    simpa [Cover.toPresieveOver] using
      (Presieve.ofArrows_pullback f (fun i â†¦ (ğ’°.obj i).asOver S) (fun i â†¦ (ğ’°.map i).asOver S)).symm
  transitive := by
    rintro X _ T âŸ¨ğ’°, h, rflâŸ© H
    choose V h hV using H
    refine âŸ¨ğ’°.bind (fun j => V ((ğ’°.map j).asOver S) âŸ¨jâŸ©), inferInstance, ?_âŸ©
    convert Presieve.ofArrows_bind _ (fun j â†¦ (ğ’°.map j).asOver S) _
      (fun Y f H j â†¦ ((V f H).obj j).asOver S) (fun Y f H j â†¦ ((V f H).map j).asOver S)
    apply hV

variable (Q : MorphismProperty Scheme.{u}) [Q.IsMultiplicative] [Q.RespectsIso]
  [Q.IsStableUnderBaseChange] [IsJointlySurjectivePreserving Q]
  [Q.HasOfPostcompProperty]

/-- The pretopology defind on the subcategory of `S`-schemes satisfying `P` where coverings
are given by `Q`-coverings in `S`-schemes satisfying `P`.
The most common case is `P = Q`. In this case, this is simply surjective families
in `S`-schemes with `P`. -/
def smallPretopology : Pretopology (P.Over âŠ¤ S) where
  coverings Y R := âˆƒ (ğ’° : Cover.{u} Q Y.left) (_ : ğ’°.Over S) (h : âˆ€ j : ğ’°.J, P (ğ’°.obj j â†˜ S)),
    R = ğ’°.toPresieveOverWithProp h
  has_isos {X Y} f := âŸ¨coverOfIsIso f.left, inferInstance, fun _ â†¦ Y.prop,
    (Presieve.ofArrows_pUnit _).symmâŸ©
  pullbacks := by
    rintro Y X f _ âŸ¨ğ’°, h, p, rflâŸ©
    refine âŸ¨ğ’°.pullbackCoverOverProp' S f.left (Q := P) Y.prop X.prop p, inferInstance, ?_, ?_âŸ©
    Â· intro j
      apply MorphismProperty.Comma.prop
    Â· simp [Cover.toPresieveOverWithProp]
      symm
      convert Presieve.ofArrows_pullback f (fun i â†¦ âŸ¨(ğ’°.obj i).asOver S, p iâŸ©)
              (fun i â†¦ âŸ¨(ğ’°.map i).asOver S, trivial, trivialâŸ©)
  transitive := by
    rintro X _ T âŸ¨ğ’°, h, p, rflâŸ© H
    choose V h pV hV using H
    let ğ’±j (j : ğ’°.J) : (Cover Q ((ğ’°.obj j).asOverProp S (p j)).left) :=
      V ((ğ’°.map j).asOverProp S) âŸ¨jâŸ©
    refine âŸ¨ğ’°.bind (fun j â†¦ ğ’±j j), inferInstance, fun j â†¦ pV _ _ _, ?_âŸ©
    convert Presieve.ofArrows_bind _ (fun j â†¦ ((ğ’°.map j).asOverProp S)) _
      (fun Y f H j â†¦ ((V f H).obj j).asOverProp S (pV _ _ _))
      (fun Y f H j â†¦ ((V f H).map j).asOverProp S)
    apply hV

abbrev overTopology : GrothendieckTopology (Over S) :=
  (Scheme.grothendieckTopology P).over S

lemma overTopology_eq_toGrothendieck_overPretopology :
    overTopology P S = (overPretopology P S).toGrothendieck := by
  ext X R
  rw [GrothendieckTopology.mem_over_iff, Pretopology.mem_toGrothendieck]
  constructor
  Â· rintro âŸ¨T, âŸ¨ğ’°, rflâŸ©, hTâŸ©
    letI (i : ğ’°.J) : OverClass (ğ’°.obj i) S := { hom := ğ’°.map i â‰« X.hom }
    letI : ğ’°.Over S := {
      over := inferInstance
      isOver_map := fun i â†¦ âŸ¨rflâŸ©
    }
    use ğ’°.toPresieveOver, âŸ¨ğ’°, inferInstance, rflâŸ©
    rwa [Cover.toPresieveOver_le_arrows_iff]
  Â· rintro âŸ¨T, âŸ¨ğ’°, h, rflâŸ©, hTâŸ©
    use Presieve.ofArrows ğ’°.obj ğ’°.map, âŸ¨ğ’°, rflâŸ©
    rwa [Cover.toPresieveOver_le_arrows_iff] at hT

lemma mem_overTopology (X : Over S) (R : Sieve X) :
    R âˆˆ S.overTopology P X â†”
      âˆƒ (ğ’° : Cover.{u} P X.left) (_ : ğ’°.Over S),
          ğ’°.toPresieveOver â‰¤ R.arrows := by
  rw [overTopology_eq_toGrothendieck_overPretopology]
  constructor
  Â· rintro âŸ¨T, âŸ¨ğ’°, h, rflâŸ©, hleâŸ©
    use ğ’°, h
  Â· rintro âŸ¨ğ’°, hğ’°, hleâŸ©
    exact âŸ¨ğ’°.toPresieveOver, âŸ¨ğ’°, hğ’°, rflâŸ©, hleâŸ©

variable {Q : MorphismProperty Scheme.{u}} [Q.IsStableUnderComposition]

variable {P} in
lemma locallyCoverDense_of_le (hPQ : P â‰¤ Q) :
    (MorphismProperty.Over.forget Q âŠ¤ S).LocallyCoverDense (overTopology P S) where
  functorPushforward_functorPullback_mem X := by
    intro âŸ¨T, hTâŸ©
    rw [mem_overTopology] at hT âŠ¢
    obtain âŸ¨ğ’°, h, hleâŸ© := hT
    use ğ’°, h
    simp only [MorphismProperty.Comma.forget_obj, Sieve.functorPushforward_apply,
      Sieve.functorPullback_apply]
    rintro - - âŸ¨iâŸ©
    have : Q (ğ’°.obj i â†˜ S) := by
      rw [â† comp_over (ğ’°.map i) S]
      exact Q.comp_mem _ _ (hPQ _ <| ğ’°.map_prop i) X.prop
    let Ui : Q.Over âŠ¤ S := âŸ¨OverClass.asOver (X := ğ’°.obj i) S, thisâŸ©
    use Ui, MorphismProperty.Over.homMk (ğ’°.map i) (comp_over (ğ’°.map i) S), ğŸ™ _
    simp only [Presieve.functorPullback_mem, MorphismProperty.Comma.forget_obj,
      MorphismProperty.Comma.forget_map, MorphismProperty.Over.homMk_hom, Category.id_comp]
    exact âŸ¨hle _ (by use i), rflâŸ©

instance : (MorphismProperty.Over.forget P âŠ¤ S).LocallyCoverDense (overTopology P S) :=
  locallyCoverDense_of_le S le_rfl

/-- If `P` and `Q` are morphism properties with `P â‰¤ Q`, this is the Grothendieck topology
induced via the forgetful functor `Q.Over âŠ¤ S â¥¤ Over S` by the topology defined by `P`. -/
abbrev smallTopologyOfLE (hPQ : P â‰¤ Q) : GrothendieckTopology (Q.Over âŠ¤ S) :=
  letI : (MorphismProperty.Over.forget Q âŠ¤ S).LocallyCoverDense (overTopology P S) :=
    locallyCoverDense_of_le S hPQ
  (MorphismProperty.Over.forget Q âŠ¤ S).inducedTopology (S.overTopology P)

variable [Q.IsMultiplicative] [Q.IsStableUnderBaseChange]
  [IsJointlySurjectivePreserving Q] [Q.HasOfPostcompProperty]

lemma _root_.CategoryTheory.Sieve.mem_functorPushforward_of_full {C D : Type*} [Category C]
    [Category D] (F : C â¥¤ D) [F.Full] {X Y : C} (R : Sieve X) (f : F.obj Y âŸ¶ F.obj X) :
    (R.functorPushforward F).arrows f â†” âˆƒ (g : Y âŸ¶ X), F.map g = f âˆ§ R g := by
  constructor
  Â· intro âŸ¨Z, g, h, hg, hcompâŸ©
    obtain âŸ¨h', hh'âŸ© := F.map_surjective h
    use h' â‰« g
    simp [hh', hcomp]
    apply R.downward_closed hg
  Â· intro âŸ¨g, hcomp, hgâŸ©
    use Y, g, ğŸ™ _, hg
    simp [hcomp]

lemma _root_.CategoryTheory.Sieve.mem_functorPushforward_of_full_of_faithful {C D : Type*}
    [Category C] [Category D]
    (F : C â¥¤ D) [F.Full] [F.Faithful] {X Y : C} (R : Sieve X) (f : Y âŸ¶ X) :
    (R.functorPushforward F).arrows (F.map f) â†” R f := by
  rw [Sieve.mem_functorPushforward_of_full]
  constructor
  Â· intro âŸ¨g, hcomp, hgâŸ©
    rwa [â† F.map_injective hcomp]
  Â· intro hf
    use f

lemma smallTopologyOfLE_eq_toGrothendieck_smallPretopology (hPQ : P â‰¤ Q) :
    smallTopologyOfLE P S hPQ = (smallPretopology Q S P).toGrothendieck := by
  ext X R
  rw [Pretopology.mem_toGrothendieck]
  simp
  rw [mem_overTopology]
  constructor
  Â· intro âŸ¨ğ’°, h, leâŸ©
    have hj (j : ğ’°.J) : Q (ğ’°.obj j â†˜ S) := by
      rw [â† comp_over (ğ’°.map j)]
      exact Q.comp_mem _ _ (hPQ _ <| ğ’°.map_prop _) X.prop
    use ğ’°.toPresieveOverWithProp hj
    constructor
    Â· use ğ’°, h, hj
    Â· rintro - - âŸ¨iâŸ©
      let fi : (ğ’°.obj i).asOverProp S (hj i) âŸ¶ X := âŸ¨(ğ’°.map i).asOver S, trivial, trivialâŸ©
      have : ğ’°.toPresieveOver ((MorphismProperty.Over.forget Q âŠ¤ S).map fi) := by use i
      have : R.functorPushforward _ ((MorphismProperty.Over.forget Q âŠ¤ S).map fi) := le _ this
      rwa [Sieve.mem_functorPushforward_of_full_of_faithful] at this
  Â· rintro âŸ¨T, âŸ¨ğ’°, h, p, rflâŸ©, leâŸ©
    use ğ’°, h
    rintro - - âŸ¨iâŸ©
    use (ğ’°.obj i).asOverProp S (p i), (ğ’°.map i).asOverProp S, ğŸ™ _
    simp only [MorphismProperty.Comma.forget_obj, MorphismProperty.Comma.forget_map,
      MorphismProperty.Comma.Hom.hom_mk, Category.id_comp, and_true]
    apply le
    use i

/-- A special case of `smallTopologyOfLE` for the most common case, where `P = Q`. -/
abbrev smallTopology : GrothendieckTopology (P.Over âŠ¤ S) :=
  (MorphismProperty.Over.forget P âŠ¤ S).inducedTopology (S.overTopology P)

lemma smallTopology_eq_toGrothendieck_smallPretopology :
    S.smallTopology P = (S.smallPretopology P P).toGrothendieck :=
  smallTopologyOfLE_eq_toGrothendieck_smallPretopology _ _ le_rfl 

lemma mem_toGrothendieck_smallPretopology (X : P.Over âŠ¤ S) (R : Sieve X) :
    R âˆˆ (S.smallPretopology P Q).toGrothendieck _ X â†”
      âˆ€ x : X.left, âˆƒ (Y : P.Over âŠ¤ S) (f : Y âŸ¶ X) (y : Y.left),
        R f âˆ§ Q f.left âˆ§ f.left.base y = x := by
  rw [Pretopology.mem_toGrothendieck]
  constructor
  Â· rintro âŸ¨T, âŸ¨ğ’°, h, p, rflâŸ©, hleâŸ©
    intro x
    obtain âŸ¨y, hyâŸ© := ğ’°.covers x
    refine âŸ¨(ğ’°.obj (ğ’°.f x)).asOverProp S (p _), (ğ’°.map (ğ’°.f x)).asOverProp S, y, hle _ ?_,
      ğ’°.map_prop _, hyâŸ©
    use ğ’°.f x
  Â· intro h
    choose Y f y hf hQ hy using h
    let ğ’° : X.left.Cover Q :=
      { J := X.left,
        obj := fun i â†¦ (Y i).left
        map := fun i â†¦ (f i).left
        map_prop := hQ
        f := id
        covers := fun i â†¦ âŸ¨y i, hy iâŸ© }
    letI : ğ’°.Over S :=
      { over := fun i â†¦ inferInstance
        isOver_map := fun i â†¦ inferInstance }
    use ğ’°.toPresieveOverWithProp fun i â†¦ MorphismProperty.Comma.prop _
    constructor
    Â· use ğ’°, inferInstance, fun i â†¦ MorphismProperty.Comma.prop _
    Â· rintro - - âŸ¨iâŸ©
      exact hf i

lemma mem_smallTopology (X : P.Over âŠ¤ S) (R : Sieve X) :
    R âˆˆ S.smallTopology P X â†”
      âˆƒ (ğ’° : Cover.{u} P X.left) (_ : ğ’°.Over S) (h : âˆ€ j, P (ğ’°.obj j â†˜ S)),
          ğ’°.toPresieveOverWithProp h â‰¤ R.arrows := by
  rw [smallTopology_eq_toGrothendieck_smallPretopology]
  constructor
  Â· rintro âŸ¨T, âŸ¨ğ’°, h, p, rflâŸ©, hleâŸ©
    use ğ’°, h, p
  Â· rintro âŸ¨ğ’°, hğ’°, p, hleâŸ©
    exact âŸ¨ğ’°.toPresieveOverWithProp p, âŸ¨ğ’°, hğ’°, p, rflâŸ©, hleâŸ©

end AlgebraicGeometry.Scheme
