/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten, JoÃ«l Riou, Adam Topaz
-/
import Mathlib.AlgebraicGeometry.Pullbacks
import Mathlib.CategoryTheory.Sites.MorphismProperty

/-!

# Site defined by a morphism property

Given a multiplicative morphism property `P` that is stable under base change, we define the
associated (pre)topology on the category of schemes, where coverings are given
by jointly surjective families of morphisms satisfying `P`.

## TODO

- Define the small site on `Over P Q X`.

-/

universe v u

open CategoryTheory MorphismProperty Limits

namespace AlgebraicGeometry.Scheme

variable (P : MorphismProperty Scheme.{u}) [P.IsMultiplicative] [P.RespectsIso]
  [P.IsStableUnderBaseChange] [IsJointlySurjectivePreserving P]

/--
The pretopology on the category of schemes defined by covering families where the components
satisfy `P`.

The coverings are defined via existence of a `P`-cover. This is convenient in practice, as one
directly has the cover available. For a pretopology generating the same Grothendieck topology, see
`AlgebraicGeometry.Scheme.grothendieckTopology_eq_inf`.
-/
def pretopology : Pretopology Scheme.{u} where
  coverings Y S := âˆƒ (U : Cover.{u} P Y), S = Presieve.ofArrows U.obj U.map
  has_isos _ _ f _ := âŸ¨coverOfIsIso f, (Presieve.ofArrows_pUnit _).symmâŸ©
  pullbacks := by
    rintro Y X f _ âŸ¨U, rflâŸ©
    exact âŸ¨U.pullbackCover' f, (Presieve.ofArrows_pullback _ _ _).symmâŸ©
  transitive := by
    rintro X _ T âŸ¨U, rflâŸ© H
    choose V hV using H
    use U.bind (fun j => V (U.map j) âŸ¨jâŸ©)
    simpa only [Cover.bind, â† hV] using Presieve.ofArrows_bind U.obj U.map _
      (fun _ f H => (V f H).obj) (fun _ f H => (V f H).map)

/-- The Grothendieck topology on the category of schemes induced by the pretopology defined by
`P`-covers. -/
abbrev grothendieckTopology : GrothendieckTopology Scheme.{u} :=
  (pretopology P).toGrothendieck

/-- The pretopology on the category of schemes defined by jointly surjective families.

Note: The assumption `IsJointlySurjectivePreserving âŠ¤` is mathematically unneeded, and only here
to reduce imports. To satisfy it, use `AlgebraicGeometry.Scheme.isJointlySurjectivePreserving`. -/
def surjectiveFamiliesPretopology [IsJointlySurjectivePreserving âŠ¤] : Pretopology Scheme.{u} where
  coverings X S :=
    âˆ€ x : X, âˆƒ (Y : Scheme.{u}) (y : Y) (f : Y âŸ¶ X) (hf : S f), f.base y = x
  has_isos X Y f hf x := by
    use Y, (inv f).base x, f
    simp [â† Scheme.comp_base_apply]
  pullbacks X Y f S hS x := by
    obtain âŸ¨Z, z, g, hg, hzâŸ© := hS (f.base x)
    obtain âŸ¨w, hwâŸ© :=
      IsJointlySurjectivePreserving.exists_preimage_snd_triplet_of_prop (P := âŠ¤) trivial z x hz
    use pullback g f, w, pullback.snd g f
    simpa [hw] using Presieve.pullbackArrows.mk Z g hg
  transitive X S T hS hT x := by
    obtain âŸ¨Y, y, f, hf, hyâŸ© := hS x
    obtain âŸ¨Z, z, g, hg, hzâŸ© := hT f hf y
    use Z, z, g â‰« f
    simpa [hz, hy] using Presieve.bind_comp f hf hg

lemma pretopology_le_inf [IsJointlySurjectivePreserving âŠ¤] :
    pretopology P â‰¤ surjectiveFamiliesPretopology âŠ“ P.pretopology := by
  rintro X S âŸ¨ğ’°, rflâŸ©
  refine âŸ¨fun x â†¦ ?_, fun âŸ¨iâŸ© â†¦ ğ’°.map_prop iâŸ©
  obtain âŸ¨a, haâŸ© := ğ’°.covers x
  refine âŸ¨ğ’°.obj (ğ’°.f x), a, ğ’°.map (ğ’°.f x), âŸ¨_âŸ©, haâŸ©

/--
The Grothendieck topology defined by `P`-covers agrees with the Grothendieck
topology induced by the intersection of the pretopology of surjective families with
the pretopology defined by `P`.

Note: Because of size issues, this does not hold on the level of pretopologies: A presieve
in the intersection can have up to `Type (u + 1)` many components, while in the definition
of `AlgebraicGeometry.Scheme.pretopology` we only allow `Type u` many components.
-/
lemma grothendieckTopology_eq_inf [IsJointlySurjectivePreserving âŠ¤] :
    grothendieckTopology P = (surjectiveFamiliesPretopology âŠ“ P.pretopology).toGrothendieck := by
  apply le_antisymm ((Pretopology.gi Scheme.{u}).gc.monotone_l (pretopology_le_inf P))
  intro X S âŸ¨T, âŸ¨hs, hPâŸ©, hleâŸ©
  let _ : Type (u + 1) := Presieve X
  let J := (Y : Scheme.{u}) Ã— (Y âŸ¶ X)
  choose Y y f hf hy using hs
  let ğ’° : Cover.{u} P X :=
    { J := X
      obj := Y
      map := f
      f := id
      covers := fun x â†¦ âŸ¨y x, hy xâŸ©
      map_prop := fun x â†¦ hP (hf x)
    }
  refine âŸ¨Presieve.ofArrows ğ’°.obj ğ’°.map, âŸ¨ğ’°, rflâŸ©, ?_âŸ©
  rintro Z g âŸ¨xâŸ©
  exact hle _ (hf x)

variable {P}

lemma pretopology_cover {Y : Scheme.{u}} (ğ’° : Cover.{u} P Y) :
    pretopology P Y (Presieve.ofArrows ğ’°.obj ğ’°.map) :=
  âŸ¨ğ’°, rflâŸ©

lemma grothendieckTopology_cover {X : Scheme.{u}} (ğ’° : Cover.{v} P X) :
    grothendieckTopology P X (Sieve.generate (Presieve.ofArrows ğ’°.obj ğ’°.map)) := by
  let ğ’± : Cover.{u} P X :=
    { J := X
      obj := fun x â†¦ ğ’°.obj (ğ’°.f x)
      map := fun x â†¦ ğ’°.map (ğ’°.f x)
      f := id
      covers := ğ’°.covers
      map_prop := fun _ â†¦ ğ’°.map_prop _
    }
  refine âŸ¨_, pretopology_cover ğ’±, ?_âŸ©
  rintro _ _ âŸ¨yâŸ©
  exact âŸ¨_, ğŸ™ _, ğ’°.map (ğ’°.f y), âŸ¨_âŸ©, by simp [ğ’±]âŸ©

section

variable {Q : MorphismProperty Scheme.{u}} [Q.IsMultiplicative] [Q.RespectsIso]
  [Q.IsStableUnderBaseChange] [IsJointlySurjectivePreserving Q]

lemma pretopology_le_pretopology (hPQ : P â‰¤ Q) :
    pretopology P â‰¤ pretopology Q := by
  rintro X - âŸ¨ğ’°, rflâŸ©
  use ğ’°.changeProp Q (fun j â†¦ hPQ _ (ğ’°.map_prop j))
  rfl

lemma grothendieckTopology_le_grothendieckTopology (hPQ : P â‰¤ Q) :
    grothendieckTopology P â‰¤ grothendieckTopology Q :=
  (Pretopology.gi Scheme.{u}).gc.monotone_l (pretopology_le_pretopology hPQ)

end

end AlgebraicGeometry.Scheme
