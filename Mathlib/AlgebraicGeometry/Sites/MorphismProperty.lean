/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten, Jo√´l Riou, Adam Topaz
-/
import Mathlib.AlgebraicGeometry.Pullbacks
import Mathlib.CategoryTheory.Sites.MorphismProperty

/-!

# Site defined by a morphism property

Given a multiplicative morphism property `P` that is stable under base change, we define the
associated (pre)topology on the category of schemes, where coverings are given
by jointly surjective families of morphisms satisfying `P`.

## TODO

- Define the small site on `Over P Q X`.

-/

universe v u

open CategoryTheory MorphismProperty Limits

namespace AlgebraicGeometry.Scheme

variable (P : MorphismProperty Scheme.{u}) [P.IsMultiplicative] [P.RespectsIso]
  [P.IsStableUnderBaseChange] [IsJointlySurjectivePreserving P]

/--
The pretopology on the category of schemes defined by covering families where the components
satisfy `P`.

The coverings are defined via existence of a `P`-cover. This is convenient in practice, as one
directly has the cover available. For a pretopology generating the same Grothendieck topology, see
`AlgebraicGeometry.Scheme.grothendieckTopology_eq_inf`.
-/
def pretopology : Pretopology Scheme.{u} where
  coverings Y S := ‚àÉ (U : Cover.{u} P Y), S = Presieve.ofArrows U.X U.f
  has_isos _ _ f _ := ‚ü®coverOfIsIso f, (Presieve.ofArrows_pUnit _).symm‚ü©
  pullbacks := by
    rintro Y X f _ ‚ü®U, rfl‚ü©
    exact ‚ü®U.pullbackCover' f, (Presieve.ofArrows_pullback _ _ _).symm‚ü©
  transitive := by
    rintro X _ T ‚ü®U, rfl‚ü© H
    choose V hV using H
    use U.bind (fun j => V (U.f j) ‚ü®j‚ü©)
    simpa only [Cover.bind, ‚Üê hV] using Presieve.ofArrows_bind U.X U.f _
      (fun _ f H => (V f H).X) (fun _ f H => (V f H).f)

/-- The Grothendieck topology on the category of schemes induced by the pretopology defined by
`P`-covers. -/
abbrev grothendieckTopology : GrothendieckTopology Scheme.{u} :=
  (pretopology P).toGrothendieck

/-- The pretopology on the category of schemes defined by jointly surjective families.

Note: The assumption `IsJointlySurjectivePreserving ‚ä§` is mathematically unneeded, and only here
to reduce imports. To satisfy it, use `AlgebraicGeometry.Scheme.isJointlySurjectivePreserving`. -/
def jointlySurjectivePretopology [IsJointlySurjectivePreserving ‚ä§] : Pretopology Scheme.{u} where
  coverings X S :=
    ‚àÄ x : X, ‚àÉ (Y : Scheme.{u}) (y : Y) (f : Y ‚ü∂ X) (hf : S f), f.base y = x
  has_isos X Y f hf x := by
    use Y, (inv f).base x, f
    simp [‚Üê Scheme.comp_base_apply]
  pullbacks X Y f S hS x := by
    obtain ‚ü®Z, z, g, hg, hz‚ü© := hS (f.base x)
    obtain ‚ü®w, hw‚ü© :=
      IsJointlySurjectivePreserving.exists_preimage_snd_triplet_of_prop (P := ‚ä§) trivial z x hz
    use pullback g f, w, pullback.snd g f
    simpa [hw] using Presieve.pullbackArrows.mk Z g hg
  transitive X S T hS hT x := by
    obtain ‚ü®Y, y, f, hf, hy‚ü© := hS x
    obtain ‚ü®Z, z, g, hg, hz‚ü© := hT f hf y
    use Z, z, g ‚â´ f
    simpa [hz, hy] using Presieve.bind_comp f hf hg

@[deprecated (since := "2025-08-18")] alias surjectiveFamiliesPretopology :=
  jointlySurjectivePretopology

/-- The jointly surjective topology on `Scheme` is defined by the same condition as the jointly
surjective pretopology. -/
def jointlySurjectiveTopology [IsJointlySurjectivePreserving ‚ä§] :
    GrothendieckTopology Scheme.{u} :=
  jointlySurjectivePretopology.toGrothendieck.copy (fun X s ‚Ü¶ jointlySurjectivePretopology X ‚Üës) <|
    funext fun _ ‚Ü¶ Set.ext fun s ‚Ü¶
      ‚ü®fun ‚ü®_, hp, hps‚ü© x ‚Ü¶ let ‚ü®Y, y, u, hu, hyx‚ü© := hp x; ‚ü®Y, y, u, hps _ hu, hyx‚ü©,
      fun hs ‚Ü¶ ‚ü®s, hs, le_rfl‚ü©‚ü©

theorem mem_jointlySurjectiveTopology_iff_jointlySurjectivePretopology
    [IsJointlySurjectivePreserving ‚ä§] {X : Scheme.{u}} {s : Sieve X} :
    s ‚àà jointlySurjectiveTopology X ‚Üî jointlySurjectivePretopology X ‚Üës :=
  Iff.rfl

lemma jointlySurjectiveTopology_eq_toGrothendieck_jointlySurjectivePretopology
    [IsJointlySurjectivePreserving ‚ä§] :
    jointlySurjectiveTopology.{u} = jointlySurjectivePretopology.toGrothendieck :=
  GrothendieckTopology.copy_eq

lemma pretopology_le_inf [IsJointlySurjectivePreserving ‚ä§] :
    pretopology P ‚â§ jointlySurjectivePretopology ‚äì P.pretopology := by
  rintro X S ‚ü®ùí∞, rfl‚ü©
  refine ‚ü®fun x ‚Ü¶ ?_, fun _ _ ‚ü®i‚ü© ‚Ü¶ ùí∞.map_prop i‚ü©
  obtain ‚ü®a, ha‚ü© := ùí∞.covers x
  refine ‚ü®ùí∞.X (ùí∞.idx x), a, ùí∞.f (ùí∞.idx x), ‚ü®_‚ü©, ha‚ü©

/--
The Grothendieck topology defined by `P`-covers agrees with the Grothendieck
topology induced by the intersection of the pretopology of surjective families with
the pretopology defined by `P`.

Note: Because of size issues, this does not hold on the level of pretopologies: A presieve
in the intersection can have up to `Type (u + 1)` many components, while in the definition
of `AlgebraicGeometry.Scheme.pretopology` we only allow `Type u` many components.
-/
lemma grothendieckTopology_eq_inf [IsJointlySurjectivePreserving ‚ä§] :
    grothendieckTopology P = (jointlySurjectivePretopology ‚äì P.pretopology).toGrothendieck := by
  apply le_antisymm ((Pretopology.gi Scheme.{u}).gc.monotone_l (pretopology_le_inf P))
  intro X S ‚ü®T, ‚ü®hs, hP‚ü©, hle‚ü©
  let _ : Type (u + 1) := Presieve X
  let J := (Y : Scheme.{u}) √ó (Y ‚ü∂ X)
  choose Y y f hf hy using hs
  let ùí∞ : Cover.{u} P X :=
    { I‚ÇÄ := X
      X := Y
      f := f
      idx := id
      covers := fun x ‚Ü¶ ‚ü®y x, hy x‚ü©
      map_prop := fun x ‚Ü¶ hP (hf x)
    }
  refine ‚ü®Presieve.ofArrows ùí∞.X ùí∞.f, ‚ü®ùí∞, rfl‚ü©, ?_‚ü©
  rintro Z g ‚ü®x‚ü©
  exact hle _ (hf x)

variable {P}

lemma pretopology_cover {Y : Scheme.{u}} (ùí∞ : Cover.{u} P Y) :
    pretopology P Y (Presieve.ofArrows ùí∞.X ùí∞.f) :=
  ‚ü®ùí∞, rfl‚ü©

lemma grothendieckTopology_cover {X : Scheme.{u}} (ùí∞ : Cover.{v} P X) :
    grothendieckTopology P X (Sieve.generate (Presieve.ofArrows ùí∞.X ùí∞.f)) := by
  let ùí± : Cover.{u} P X :=
    { I‚ÇÄ := X
      X := fun x ‚Ü¶ ùí∞.X (ùí∞.idx x)
      f := fun x ‚Ü¶ ùí∞.f (ùí∞.idx x)
      idx := id
      covers := ùí∞.covers
      map_prop := fun _ ‚Ü¶ ùí∞.map_prop _
    }
  refine ‚ü®_, pretopology_cover ùí±, ?_‚ü©
  rintro _ _ ‚ü®y‚ü©
  exact ‚ü®_, ùüô _, ùí∞.f (ùí∞.idx y), ‚ü®_‚ü©, by simp [ùí±]‚ü©

section

variable {Q : MorphismProperty Scheme.{u}} [Q.IsMultiplicative] [Q.RespectsIso]
  [Q.IsStableUnderBaseChange] [IsJointlySurjectivePreserving Q]

lemma pretopology_le_pretopology (hPQ : P ‚â§ Q) :
    pretopology P ‚â§ pretopology Q := by
  rintro X - ‚ü®ùí∞, rfl‚ü©
  use ùí∞.changeProp Q (fun j ‚Ü¶ hPQ _ (ùí∞.map_prop j))
  rfl

lemma grothendieckTopology_le_grothendieckTopology (hPQ : P ‚â§ Q) :
    grothendieckTopology P ‚â§ grothendieckTopology Q :=
  (Pretopology.gi Scheme.{u}).gc.monotone_l (pretopology_le_pretopology hPQ)

end

end AlgebraicGeometry.Scheme
