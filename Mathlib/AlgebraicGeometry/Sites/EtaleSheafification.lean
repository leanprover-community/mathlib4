/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.AlgebraicGeometry.Sites.Etale
import Mathlib.CategoryTheory.Sites.OneHypercoverDense

/-!
# √âtale sheafification

If `S : Scheme.{u}`, the type of objects in the small √©tale site `SmallEtaleSite S` of `S`
is in `Type (u + 1)` (this is a large category), so that a priori, we may only get
sheafification for √©tale sheaves only with values in categories like `Type (u + 1)`.

In this file, we obtain the instance `HasSheafify (smallEtaleTopology S) (Type u)`
by showing that the is a "very small √©tale site" given by a small category
(whose objects consist of a certain packaging of schemes `X ‚ü∂ S` which are √©tale
and finitely presented over an open subset of `S`)

-/

universe v u

open CategoryTheory Limits

namespace CategoryTheory.Sieve

@[simp]
lemma ofArrows_le {C : Type*} [Category C] {I : Type*} {X‚ÇÄ : C} {X : I ‚Üí C} (f : ‚àÄ i, X i ‚ü∂ X‚ÇÄ)
    (S : Sieve X‚ÇÄ) : ofArrows _ f ‚â§ S ‚Üî ‚àÄ (i : I), S (f i) := by
  constructor
  ¬∑ intro h i
    exact h _ (ofArrows_mk _ f i)
  ¬∑ rintro h Y _ ‚ü®_, b, _, h, rfl‚ü©
    cases' h with i
    exact S.downward_closed (h i) _

end CategoryTheory.Sieve

namespace AlgebraicGeometry.Scheme

variable (S : Scheme.{u})

abbrev SmallEtaleSite := MorphismProperty.Over @IsEtale ‚ä§ S

variable {S}

structure IsEtaleCovering
    {I : Type v} {X : I ‚Üí Scheme.{u}} (f : ‚àÄ i, X i ‚ü∂ S) : Prop where
  isEtale (i : I) : IsEtale (f i) := by infer_instance
  jointly_surjective (s : S) : ‚àÉ i x, (f i).base x = s

namespace IsEtaleCovering

variable {I : Type v} {X : I ‚Üí Scheme.{u}} {f : ‚àÄ i, X i ‚ü∂ S}
  (h : IsEtaleCovering f)

noncomputable def liftIndex (s : S) : I := (h.jointly_surjective s).choose

noncomputable def lift (s : S) : X (h.liftIndex s) :=
  (h.jointly_surjective s).choose_spec.choose

@[simp]
lemma base_lift (s : S) : (f _).base (h.lift s) = s :=
  (h.jointly_surjective s).choose_spec.choose_spec

def sieve
    {X : S.SmallEtaleSite} {I : Type v} {Y : I ‚Üí Scheme.{u}} (f : ‚àÄ i, Y i ‚ü∂ X.left)
    (h : IsEtaleCovering f) :
    Sieve X :=
  Sieve.ofArrows (fun i ‚Ü¶ MorphismProperty.Over.mk ‚ä§ (f i ‚â´ X.hom)
    (MorphismProperty.comp_mem _ _ _ (h.isEtale i) X.prop))
      (fun i ‚Ü¶ MorphismProperty.Over.homMk (f i))

/-- Variant of `IsEtaleCovering.sieve` where we do not assume that the target scheme
is given by an object in `S.SmallEtaleSite`. -/
def sieve'
    {X : Scheme.{u}}
    {I : Type v} {Y : I ‚Üí Scheme.{u}} {f : ‚àÄ i, Y i ‚ü∂ X}
    (h : IsEtaleCovering f) (a : X ‚ü∂ S) [IsEtale a] :
    Sieve (MorphismProperty.Over.mk _ a inferInstance : SmallEtaleSite S) :=
  IsEtaleCovering.sieve f h

end IsEtaleCovering

variable (S) in
-- we should have this in some form after #19096 and #18945 by Christian Merten
def smallEtaleTopology : GrothendieckTopology (SmallEtaleSite S) where
  sieves X U := ‚àÉ (I : Type u) (Y : I ‚Üí Scheme.{u}) (f : ‚àÄ i, Y i ‚ü∂ X.left)
      (hf : IsEtaleCovering f), hf.sieve ‚â§ U
  top_mem' := sorry
  pullback_stable' := sorry
  transitive' := sorry

lemma IsEtaleCovering.sieve_mem
    {X : S.SmallEtaleSite} {I : Type v} {Y : I ‚Üí Scheme.{u}} (f : ‚àÄ i, Y i ‚ü∂ X.left)
    (h : IsEtaleCovering f) : h.sieve ‚àà smallEtaleTopology S X := by
    refine ‚ü®X.left, fun x ‚Ü¶ Y (h.liftIndex x), fun x ‚Ü¶ f (h.liftIndex x),
      ‚ü®fun _ ‚Ü¶ h.isEtale _, fun x ‚Ü¶ ‚ü®x, h.lift x, by simp‚ü©‚ü©, ?_‚ü©
    dsimp [sieve]
    simp only [Sieve.ofArrows_le]
    intro x
    apply Sieve.ofArrows_mk

lemma IsEtaleCovering.sieve'_mem
    {X : Scheme.{u}}
    {I : Type v} {Y : I ‚Üí Scheme.{u}} {f : ‚àÄ i, Y i ‚ü∂ X}
    (h : IsEtaleCovering f) (a : X ‚ü∂ S) [IsEtale a] :
    h.sieve' a ‚àà smallEtaleTopology S _ := by
  apply sieve_mem

variable (S) in
structure FinitelyPresentedOverAffineOpen : Type u where
  U : Opens S
  hU : IsAffineOpen U
  g : ‚Ñï
  r : ‚Ñï
  var (x : Fin r) : MvPolynomial (Fin g) Œì(S, U)

namespace FinitelyPresentedOverAffineOpen

variable (P : FinitelyPresentedOverAffineOpen S)

noncomputable def scheme : Scheme.{u} :=
  Spec (.of (MvPolynomial (Fin P.g) Œì(S, P.U) ‚ß∏ Ideal.span (Set.range P.var)))

instance : IsAffine P.scheme := by
  dsimp [scheme]
  infer_instance

noncomputable abbrev ringHom :
    Œì(S, P.U) ‚Üí+* MvPolynomial (Fin P.g) Œì(S, P.U) ‚ß∏ Ideal.span (Set.range P.var) :=
  Algebra.toRingHom

noncomputable def œÄ : P.scheme ‚ü∂ P.U :=
  Spec.map P.ringHom ‚â´ P.hU.isoSpec.inv

noncomputable def a : P.scheme ‚ü∂ S := P.œÄ ‚â´ P.U.Œπ

@[reassoc (attr := simp)]
lemma fac : P.œÄ ‚â´ P.U.Œπ = P.a := rfl

end FinitelyPresentedOverAffineOpen

variable (S) in
structure VerySmallEtaleSite extends FinitelyPresentedOverAffineOpen S : Type u where
  isEtale_œÄ : IsEtale toFinitelyPresentedOverAffineOpen.œÄ

namespace VerySmallEtaleSite

attribute [instance] isEtale_œÄ

section

variable (X : VerySmallEtaleSite S)

instance isEtale_a : IsEtale X.a := by
  rw [‚Üê X.fac]
  infer_instance

@[simps! left hom]
noncomputable def toSmallEtaleSite : SmallEtaleSite S :=
  MorphismProperty.Over.mk ‚ä§ X.a inferInstance

end

noncomputable instance : Category (VerySmallEtaleSite S) :=
  InducedCategory.category toSmallEtaleSite

noncomputable def homMk {X Y : VerySmallEtaleSite S} (f : X.scheme ‚ü∂ Y.scheme)
    (hf : f ‚â´ Y.a = X.a := by aesop_cat) : X ‚ü∂ Y :=
  MorphismProperty.Over.homMk f hf (by simp)

end VerySmallEtaleSite

variable (S)
noncomputable def fromVerySmallEtaleSiteFunctor :
    VerySmallEtaleSite S ‚•§ SmallEtaleSite S :=
  inducedFunctor _

noncomputable def fullyFaithfulFromVerySmallEtaleSiteFunctor :
    (fromVerySmallEtaleSiteFunctor S).FullyFaithful :=
  fullyFaithfulInducedFunctor _

instance : (fromVerySmallEtaleSiteFunctor S).Full :=
  (fullyFaithfulFromVerySmallEtaleSiteFunctor S).full

instance : (fromVerySmallEtaleSiteFunctor S).Faithful :=
  (fullyFaithfulFromVerySmallEtaleSiteFunctor S).faithful

variable {S}

@[simp]
lemma fromVerySmallEtaleSiteFunctor_obj_left (X : VerySmallEtaleSite S) :
    ((fromVerySmallEtaleSiteFunctor S).obj X).left = X.scheme := rfl

@[simp]
lemma fromVerySmallEtaleSiteFunctor_obj_hom (X : VerySmallEtaleSite S) :
    ((fromVerySmallEtaleSiteFunctor S).obj X).hom = X.a := rfl

@[simp]
lemma fromVerySmallEtaleSiteFunctor_map_homMk_left
    {X Y : VerySmallEtaleSite S} (f : X.scheme ‚ü∂ Y.scheme)
    (hf : f ‚â´ Y.a = X.a := by aesop_cat) :
    ((fromVerySmallEtaleSiteFunctor S).map (VerySmallEtaleSite.homMk f hf)).left = f :=
  rfl

variable (S) in
def verySmallEtaleTopology : GrothendieckTopology (VerySmallEtaleSite S) where
  sieves X U := Sieve.functorPushforward (fromVerySmallEtaleSiteFunctor S) U
    ‚àà smallEtaleTopology S _
  top_mem' X := by
    dsimp [Membership.mem, Set.Mem]
    simp only [Sieve.functorPushforward_top]
    exact (smallEtaleTopology (S := S)).top_mem X.toSmallEtaleSite
  pullback_stable' := sorry
  transitive' := sorry

namespace fromVerySmallEtaleSiteFunctor_isOneHypercoverDense

variable {X : Scheme.{u}} (f : X ‚ü∂ S)

lemma exists_etale_neighborhood [IsEtale f] (x : X) :
    ‚àÉ (U : VerySmallEtaleSite S) (œÄ : U.scheme ‚ü∂ X) (hœÄ : IsEtale œÄ)
      (fac : œÄ ‚â´ f = U.a) (u : U.scheme), œÄ.base u = x := sorry
  -- hœÄ is automatic, but does mathlib already know?

variable [IsEtale f]

section

variable (x : X)

noncomputable def nbd : VerySmallEtaleSite S :=
  (exists_etale_neighborhood f x).choose

noncomputable def nbdœÄ : (nbd f x).scheme ‚ü∂ X :=
  (exists_etale_neighborhood f x).choose_spec.choose

instance : IsEtale (nbdœÄ f x) :=
  (exists_etale_neighborhood f x).choose_spec.choose_spec.choose

@[reassoc (attr := simp)]
lemma fac : nbdœÄ f x ‚â´ f = (nbd f x).a :=
  (exists_etale_neighborhood f x).choose_spec.choose_spec.choose_spec.choose

noncomputable def liftNbd : (nbd f x).scheme :=
  (exists_etale_neighborhood f x).choose_spec.choose_spec.choose_spec.choose_spec.choose

@[simp]
lemma liftNbd_fac :
    (nbdœÄ f x).base (liftNbd f x) = x :=
  (exists_etale_neighborhood f x).choose_spec.choose_spec.choose_spec.choose_spec.choose_spec

end

lemma isEtaleCovering : IsEtaleCovering (nbdœÄ f) where
  isEtale i := inferInstance
  jointly_surjective x := ‚ü®x, liftNbd f x, by simp‚ü©

section

instance (X Y S : Scheme.{u}) (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [IsEtale g] :
    IsEtale (pullback.fst f g) :=
  MorphismProperty.pullback_fst f g inferInstance

instance (X Y S : Scheme.{u}) (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [IsEtale f] :
    IsEtale (pullback.snd f g) :=
  MorphismProperty.pullback_snd f g inferInstance

end

@[simps]
noncomputable def preOneHypercoverDenseData :
    (S.fromVerySmallEtaleSiteFunctor.PreOneHypercoverDenseData
      (MorphismProperty.Over.mk _ f inferInstance)) where
  I‚ÇÄ := X
  X x := nbd f x
  f x := MorphismProperty.Over.homMk (nbdœÄ f x)
  I‚ÇÅ x‚ÇÅ x‚ÇÇ := (pullback (nbdœÄ f x‚ÇÅ) (nbdœÄ f x‚ÇÇ) : Scheme.{u})
  Y x‚ÇÅ x‚ÇÇ x‚ÇÅ‚ÇÇ := nbd (pullback.fst _ _ ‚â´ (nbd f x‚ÇÅ).a) x‚ÇÅ‚ÇÇ
  p‚ÇÅ x‚ÇÅ x‚ÇÇ x‚ÇÅ‚ÇÇ := MorphismProperty.Over.homMk (nbdœÄ _ _ ‚â´ pullback.fst _ _)
  p‚ÇÇ x‚ÇÅ x‚ÇÇ x‚ÇÅ‚ÇÇ := MorphismProperty.Over.homMk (nbdœÄ _ _ ‚â´ pullback.snd _ _) (by
    have := pullback.condition (f := nbdœÄ f x‚ÇÅ) (g := nbdœÄ f x‚ÇÇ) =‚â´ f
    simp only [Category.assoc, fac] at this
    dsimp
    rw [Category.assoc, ‚Üê this, fac])
  w x‚ÇÅ x‚ÇÇ x‚ÇÅ‚ÇÇ := by
    dsimp
    ext : 2
    ¬∑ dsimp [fromVerySmallEtaleSiteFunctor]
      simp only [Category.assoc, pullback.condition]
    ¬∑ dsimp

noncomputable def oneHypercoverDenseData :
    (S.fromVerySmallEtaleSiteFunctor.OneHypercoverDenseData (verySmallEtaleTopology S)
      (smallEtaleTopology S) (MorphismProperty.Over.mk _ f inferInstance)) where
  toPreOneHypercoverDenseData := preOneHypercoverDenseData f
  mem‚ÇÄ := by
    refine GrothendieckTopology.superset_covering _ ?_ ((isEtaleCovering f).sieve'_mem f)
    dsimp [IsEtaleCovering.sieve', IsEtaleCovering.sieve]
    simp only [Sieve.ofArrows_le]
    intro x
    refine ‚ü®(nbd f x).toSmallEtaleSite, MorphismProperty.Over.homMk (by exact ùüô _), _, ‚ü®x‚ü©,
      by aesop_cat‚ü©
  mem‚ÇÅ‚ÇÄ := by
    -- reduce to the case of the pullback and proceed as above
    sorry

end fromVerySmallEtaleSiteFunctor_isOneHypercoverDense

instance fromVerySmallEtaleSiteFunctor_isOneHypercoverDense :
    Functor.IsOneHypercoverDense.{u} (fromVerySmallEtaleSiteFunctor S)
      (verySmallEtaleTopology S) (smallEtaleTopology S) where
  nonempty_oneHypercoverDenseData X := by
    have := X.prop
    exact ‚ü®fromVerySmallEtaleSiteFunctor_isOneHypercoverDense.oneHypercoverDenseData X.hom‚ü©


instance : Functor.IsDenseSubsite (verySmallEtaleTopology S) (smallEtaleTopology S)
    (fromVerySmallEtaleSiteFunctor S) :=
  Functor.isDenseSubsite_of_isOneHypercoverDense _ _ _ (by rfl)

example : HasSheafify (smallEtaleTopology S) (Type (u + 1)) := inferInstance

example : HasSheafify (verySmallEtaleTopology S) (Type u) := inferInstance

-- the main result
instance : HasSheafify (smallEtaleTopology S) (Type u) :=
  Functor.hasSheafify_of_isOneHypercoverDense.{u}
    (fromVerySmallEtaleSiteFunctor S)
      (verySmallEtaleTopology S) (smallEtaleTopology S) (Type u)

end AlgebraicGeometry.Scheme
