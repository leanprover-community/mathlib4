/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
module

public import Mathlib.AlgebraicGeometry.Morphisms.Etale
public import Mathlib.AlgebraicGeometry.PullbackCarrier
public import Mathlib.AlgebraicGeometry.Sites.BigZariski
public import Mathlib.AlgebraicGeometry.Sites.Small
public import Mathlib.CategoryTheory.Sites.Point.Basic
public import Mathlib.FieldTheory.IsSepClosed
public import Mathlib.CategoryTheory.Functor.TypeValuedFlat

/-!

# The √©tale site

In this file we define the big √©tale site, i.e. the √©tale topology as a Grothendieck topology
on the category of schemes.

-/

@[expose] public section

universe v u

open CategoryTheory MorphismProperty Limits

namespace AlgebraicGeometry.Scheme

/-- Big √©tale site: the √©tale pretopology on the category of schemes. -/
def etalePretopology : Pretopology Scheme.{u} :=
  pretopology @IsEtale

/-- Big √©tale site: the √©tale topology on the category of schemes. -/
abbrev etaleTopology : GrothendieckTopology Scheme.{u} :=
  grothendieckTopology @IsEtale

lemma zariskiTopology_le_etaleTopology : zariskiTopology ‚â§ etaleTopology := by
  apply grothendieckTopology_monotone
  intro X Y f hf
  infer_instance

/-- The small √©tale site of a scheme is the Grothendieck topology on the
category of schemes √©tale over `X` induced from the √©tale topology on `Scheme.{u}`. -/
def smallEtaleTopology (X : Scheme.{u}) : GrothendieckTopology X.Etale :=
  X.smallGrothendieckTopology (P := @IsEtale)

/-- The pretopology generating the small √©tale site. -/
def smallEtalePretopology (X : Scheme.{u}) : Pretopology X.Etale :=
  X.smallPretopology (Q := @IsEtale) (P := @IsEtale)

instance {S : Scheme.{u}} (ùí∞ : S.Cover (precoverage @IsEtale)) (i : ùí∞.I‚ÇÄ) : IsEtale (ùí∞.f i) :=
  ùí∞.map_prop i

section Points

@[simps]
def _root_.CategoryTheory.Functor.Elements.initial' {C : Type*} [Category C] (A : C·µí·µñ) :
    (coyoneda.obj A).Elements :=
  ‚ü®A.unop, ùüô _‚ü©

def _root_.CategoryTheory.Functor.Elements.isInitial' {C : Type*} [Category C] (A : C·µí·µñ) :
    IsInitial (Functor.Elements.initial' A) :=
  .ofUniqueHom (fun X ‚Ü¶ ‚ü®X.2, by simp‚ü©) <| by rintro Y ‚ü®_, h‚ü©; ext; simpa using h

instance {C : Type*} [Category C] (A : C·µí·µñ) : HasInitial ((coyoneda.obj A).Elements) :=
  (Functor.Elements.isInitial' A).hasInitial

instance {C : Type*} [Category C] (A : C) : HasInitial ((yoneda.obj A).Elements) :=
  (Functor.Elements.isInitial A).hasInitial

instance (C : Type*) [Category C] [HasInitial C] : InitiallySmall.{u} C :=
  have := Functor.initial_const_initial (C := PUnit.{u + 1}) (D := C)
  .mk' ((Functor.const PUnit.{u + 1}).obj (‚ä•_ C))

instance (C : Type*) [Category C] [HasTerminal C] : FinallySmall.{u} C :=
  have := Functor.final_const_terminal (C := PUnit.{u + 1}) (D := C)
  .mk' ((Functor.const PUnit.{u + 1}).obj (‚ä§_ C))

/-- A separably closed field `Œ©` defines a point on the √©tale topology by the fiber
functor `X ‚Ü¶ Hom(Spec Œ©, X)`. -/
def geometricFiber (Œ© : Type u) [Field Œ©] [IsSepClosed Œ©] : etaleTopology.Point where
  fiber := coyoneda.obj ‚ü®Spec (.of Œ©)‚ü©
  jointly_surjective {S} R hR (f : Spec (.of Œ©) ‚ü∂ S) := by
    obtain ‚ü®‚ü®x, a‚ü©, rfl‚ü© := (Scheme.SpecToEquivOfField Œ© S).symm.surjective f
    rw [mem_grothendieckTopology_iff] at hR
    obtain ‚ü®ùí∞, hle‚ü© := hR
    obtain ‚ü®i, y, rfl‚ü© := ùí∞.exists_eq x
    refine ‚ü®ùí∞.X i, ùí∞.f i, hle _ ‚ü®i‚ü©, ?_‚ü©
    let k := (ùí∞.X i).residueField y
    let m : S.residueField (ùí∞.f i y) ‚ü∂ (ùí∞.X i).residueField y :=
      (ùí∞.f i).residueFieldMap y
    algebraize [((ùí∞.f i).residueFieldMap y).hom, a.hom]
    let b : (ùí∞.X i).residueField y ‚Üí‚Çê[S.residueField (ùí∞.f i y)] Œ© :=
      IsSepClosed.lift
    have hfac : (ùí∞.f i).residueFieldMap y ‚â´ CommRingCat.ofHom b.toRingHom = a := by
      ext1; exact b.comp_algebraMap
    use Spec.map (CommRingCat.ofHom b.toRingHom) ‚â´ (ùí∞.X i).fromSpecResidueField y
    simp [SpecToEquivOfField, ‚Üê hfac]
  isCofiltered := Functor.isCofiltered_elements _

end Points

end AlgebraicGeometry.Scheme
