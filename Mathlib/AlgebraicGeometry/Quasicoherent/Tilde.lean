/-
Copyright (c) 2024 Weihong Xu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Weihong Xu
-/

import Mathlib.AlgebraicGeometry.Spec
import Mathlib.Algebra.Category.ModuleCat.Sheaf

/-!

# Construction of M^~

Given any commutative ring `R` and `R`-module `M`, we construct the sheaf `M^~` of `ğ’ª_SpecR`-modules
such that `M^~(U)` is the set of dependent functions that are locally fractions.

## Main definitions

* `AlgebraicGeometry.TildeInAddCommGrp` : `M^~` as a sheaf of abelian groups.
* `AlgebraicGeometry.TildeInModules` : `M^~` as a sheaf of `ğ’ª_{Spec R}`-modules.

-/

universe u

open TopCat AlgebraicGeometry TopologicalSpace CategoryTheory Opposite

variable (R : Type u) [CommRing R] (M : Type u) [AddCommGroup M] [Module R M]

namespace AlgebraicGeometry

namespace Tilde

/-- For an `R`-module `M` and a point `P` in `Spec R`, `Localizations P` is the localized module
`M` at the prime ideal `P`. -/
abbrev Localizations (P : PrimeSpectrum.Top R) :=
LocalizedModule P.asIdeal.primeCompl M

/-- For any open subset `U âŠ† Spec R`, `IsFraction` is the predicate expressing that a function
`f : âˆ_{x âˆˆ U}, Mâ‚“` is such that for any `ğ”­ âˆˆ U`, `f ğ”­ = m / s` for some `m : M` and `s âˆ‰ ğ”­`.
In short `f` is a fraction on `U`. -/
def IsFraction {U : Opens (PrimeSpectrum R)} (f : âˆ€ ğ”­ : U, Localizations R M ğ”­.1) : Prop :=
  âˆƒ (m : M) (s : R),
    âˆ€ x : U, Â¬s âˆˆ x.1.asIdeal âˆ§ s â€¢ f x = LocalizedModule.mkLinearMap x.1.asIdeal.primeCompl M m

/--
The property of a function `f : âˆ_{x âˆˆ U}, Mâ‚“` being a fraction is stable under restriction.
-/
def isFractionPrelocal : PrelocalPredicate (Localizations R M) where
  pred {U} f := IsFraction R M f
  res := by rintro V U i f âŸ¨m, s, wâŸ©; exact âŸ¨m, s, fun x => w (i x)âŸ©

/--
For any open subset `U âŠ† Spec R`, `IsLocallyFraction` is the predicate expressing that a function
`f : âˆ_{x âˆˆ U}, Mâ‚“` is such that for any `ğ”­ âˆˆ U`, there exists an open neighbourhood `V âˆ‹ ğ”­`, such
that for any `ğ”® âˆˆ V`, `f ğ”® = m / s` for some `m : M` and `s âˆ‰ ğ”®`.
In short `f` is locally a fraction on `U`.
-/
def isLocallyFraction : LocalPredicate (Localizations R M) := (isFractionPrelocal R M).sheafify

end Tilde

/--
For any `R`-module `M`, `TildeInType R M` is the sheaf of set on `Spec R` whose sections on `U` are
the dependent functions that are locally fractions. This is often denoted by `M^~`.

See also `Tilde.isLocallyFraction`.
-/
def TildeInType : Sheaf (Type u) (PrimeSpectrum.Top R) :=
  subsheafToTypes (Tilde.isLocallyFraction R M)

namespace Tilde

@[simp]
theorem isLocallyFraction_pred {U : Opens (PrimeSpectrum.Top R)}
    (f : âˆ€ x : U, Localizations R M x) :
    (isLocallyFraction R M).pred f =
      âˆ€ y : U,
        âˆƒ (V : _) (_ : y.1 âˆˆ V) (i : V âŸ¶ U),
          âˆƒ (m: M) (s: R), âˆ€ x : V, Â¬s âˆˆ x.1.asIdeal âˆ§ sâ€¢ f (i x) =
            LocalizedModule.mkLinearMap x.1.asIdeal.primeCompl M m :=
  rfl

/- M_x is an O_SpecR(U)-module when x is in U -/
noncomputable instance (U : (Opens (PrimeSpectrum.Top R))áµ’áµ–) (x : U.unop):
    Module ((Spec.structureSheaf R).val.obj U) (Localizations R M â†‘x):=
  Module.compHom (R := (Localization.AtPrime x.1.asIdeal)) _
    (StructureSheaf.openToLocalization R U.unop x x.2 :
      (Spec.structureSheaf R).val.obj U â†’+* Localization.AtPrime x.1.asIdeal)

lemma sections_smul_localizations_def (U : (Opens (PrimeSpectrum.Top R))áµ’áµ–) (x : U.unop)
    (r : (Spec.structureSheaf R).val.obj U)
    (m : Localizations R M â†‘x) :
  r â€¢ m = r.1 x â€¢ m := rfl

/--
For any `R`-module `M` and any open subset `U âŠ† Spec R`, `M^~(U)` is an `ğ’ª_{Spec R}(U)`-submodule
of `âˆ_{ğ”­ âˆˆ U} M_ğ”­`. -/
def sectionsSubmodule (U : (Opens (PrimeSpectrum R))áµ’áµ–) :
    Submodule ((Spec.structureSheaf R).1.obj U) (âˆ€ x : U.unop, Localizations R M x.1) where
  carrier := { f | (isLocallyFraction R M).pred f }
  zero_mem' := by
    refine fun x => âŸ¨unop U, x.2, ğŸ™ _, 0, 1, fun y => âŸ¨?_, ?_âŸ©âŸ©
    Â· rw [â† Ideal.ne_top_iff_one]; exact y.1.isPrime.1
    Â· simp
  add_mem' := by
    intro a b ha hb x
    rcases ha x with âŸ¨Va, ma, ia, ra, sa, waâŸ©
    rcases hb x with âŸ¨Vb, mb, ib, rb, sb, wbâŸ©
    refine âŸ¨Va âŠ“ Vb, âŸ¨ma, mbâŸ©, Opens.infLELeft _ _ â‰« ia,  sbâ€¢ ra+ saâ€¢rb , sa * sb, ?_âŸ©
    intro y
    rcases wa (Opens.infLELeft _ _ y : Va) with âŸ¨nma, waâŸ©
    rcases wb (Opens.infLERight _ _ y : Vb) with âŸ¨nmb, wbâŸ©
    fconstructor
    Â· intro H; cases y.1.isPrime.mem_or_mem H <;> contradiction
    Â· simp only [Opens.coe_inf, Pi.add_apply, smul_add, map_add, LinearMapClass.map_smul]
      dsimp at wa wb âŠ¢
      rw [â† wa, â† wb, â† mul_smul, â† mul_smul]
      congr 2
      simp [mul_comm]
  smul_mem' := by
    intro r a ha x
    rcases ha x with âŸ¨Va, ma, ia, ra, sa, waâŸ©
    rcases r.2 x with âŸ¨Vr, mr, ir, rr, sr, wrâŸ©
    refine âŸ¨Va âŠ“ Vr, âŸ¨ma, mrâŸ©, Opens.infLELeft _ _ â‰« ia, rrâ€¢ra, sr*sa, ?_âŸ©
    intro y
    rcases wa (Opens.infLELeft _ _ y : Va) with âŸ¨nma, waâŸ©
    rcases wr (Opens.infLERight _ _ y) with âŸ¨nmr, wrâŸ©
    fconstructor
    Â· intro H; cases y.1.isPrime.mem_or_mem H <;> contradiction
    Â· simp only [Opens.coe_inf, Pi.smul_apply, LinearMapClass.map_smul]
      dsimp at wa wr âŠ¢
      rw [mul_comm, â† Algebra.smul_def] at wr
      rw [sections_smul_localizations_def, â† wa, â† mul_smul, â† smul_assoc, mul_comm sr,
        mul_smul, wr, mul_comm rr, Algebra.smul_def, â† map_mul]
      rfl

instance (U : (Opens (PrimeSpectrum.Top R))áµ’áµ–) :
    AddCommGroup ((TildeInType R M).1.obj U) :=
  inferInstanceAs $ AddCommGroup (sectionsSubmodule R M U)

/--
`M^~` as a presheaf of abelian groups over `Spec R`
-/
def presheafInAddCommGrp : Presheaf AddCommGrp (PrimeSpectrum.Top R) where
  obj U := AddCommGrp.of ((TildeInType R M).1.obj U)
  map {U V} i :=
    { toFun := (TildeInType R M).1.map i
      map_zero' := rfl
      map_add' := fun x y => rfl}

/--
Implementation details:
checking that after forgeting the abelian group structure of `M^~` as sheaf of abelian groups, we
get the original sheaf of sets.
-/
def presheafCompForget :
    presheafInAddCommGrp R M â‹™ forget AddCommGrp â‰… (TildeInType R M).1 :=
  NatIso.ofComponents fun U => Iso.refl _

end Tilde

/--
`M^~` as a sheaf of abelian groups over `Spec R`
-/
def TildeInAddCommGrp : Sheaf AddCommGrp (PrimeSpectrum.Top R) :=
  âŸ¨Tilde.presheafInAddCommGrp R M,
    (TopCat.Presheaf.isSheaf_iff_isSheaf_comp _ _).mpr
      (TopCat.Presheaf.isSheaf_of_iso (Tilde.presheafCompForget R M).symm (TildeInType R M).cond)âŸ©

-- `SheafOfModules` want `Sheaf ... RingCat`; but we have a `Sheaf ... CommRingCat`, so we forget.
local notation3 "ğ’ª_SpecR" =>
  sheafCompose (Opens.grothendieckTopology (PrimeSpectrum.Top R))
    (forgetâ‚‚ CommRingCat RingCat) |>.obj (Spec.structureSheaf R)

noncomputable instance (U : (Opens (PrimeSpectrum.Top R))áµ’áµ–) :
    Module ((ğ’ª_SpecR).val.obj U) ((Tilde.presheafInAddCommGrp R M).obj U) :=
  inferInstanceAs $ Module _ (Tilde.sectionsSubmodule R M U)

open Tilde in
/--
`M^~` as a sheaf of `ğ’ª_{Spec R}`-modules
-/
noncomputable def TildeInModules : SheafOfModules (ğ’ª_SpecR) where
  val := {
    presheaf := (presheafInAddCommGrp R M)
    module := inferInstance
    map_smul := by
      intro U V f r m
      dsimp [TildeInAddCommGrp, presheafInAddCommGrp, TildeInType]
      ext x
      change (Spec.structureSheaf R).val.obj U at r
      change r â€¢ (m.1 âŸ¨x.1, _âŸ©) = _
      rw [sections_smul_localizations_def]
      rfl
  }
  isSheaf := (TildeInAddCommGrp R M).2

end AlgebraicGeometry
