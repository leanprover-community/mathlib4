/-
Copyright (c) 2024 Geno Racklin Asher. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Geno Racklin Asher
-/
import Mathlib.AlgebraicGeometry.Morphisms.QuasiSeparated
import Mathlib.RingTheory.Localization.Submodule
import Mathlib.RingTheory.Spectrum.Prime.Noetherian

/-!
# Noetherian and Locally Noetherian Schemes

We introduce the concept of (locally) Noetherian schemes,
giving definitions, equivalent conditions, and basic properties.

## Main definitions

* `AlgebraicGeometry.IsLocallyNoetherian`: A scheme is locally Noetherian
  if the components of the structure sheaf at each affine open are Noetherian rings.

* `AlgebraicGeometry.IsNoetherian`: A scheme is Noetherian if it is locally Noetherian
  and quasi-compact as a topological space.

## Main results

* `AlgebraicGeometry.isLocallyNoetherian_iff_of_affine_openCover`: A scheme is locally Noetherian
  if and only if it is covered by affine opens whose sections are Noetherian rings.

* `AlgebraicGeometry.IsLocallyNoetherian.quasiSeparatedSpace`: A locally Noetherian scheme is
  quasi-separated.

* `AlgebraicGeometry.isNoetherian_iff_of_finite_affine_openCover`: A scheme is Noetherian
  if and only if it is covered by finitely many affine opens whose sections are Noetherian rings.

* `AlgebraicGeometry.IsNoetherian.noetherianSpace`: A Noetherian scheme is
  topologically a Noetherian space.

## References

* [Stacks: Noetherian Schemes](https://stacks.math.columbia.edu/tag/01OU)
* [Robin Hartshorne, *Algebraic Geometry*][Har77]

-/

universe u v

open Opposite AlgebraicGeometry Localization IsLocalization TopologicalSpace CategoryTheory

namespace AlgebraicGeometry

/-- A scheme `X` is locally Noetherian if `ğ’ªâ‚“(U)` is Noetherian for all affine `U`. -/
class IsLocallyNoetherian (X : Scheme) : Prop where
  component_noetherian : âˆ€ (U : X.affineOpens),
    IsNoetherianRing Î“(X, U) := by infer_instance

section localizationProps

variable {R : Type u} [CommRing R] (S : Finset R) (hS : Ideal.span (Î± := R) S = âŠ¤)
  (hN : âˆ€ s : S, IsNoetherianRing (Away (M := R) s))

include hS hN in
/-- Let `R` be a ring, and `f i` a finite collection of elements of `R` generating the unit ideal.
If the localization of `R` at each `f i` is Noetherian, so is `R`.

We follow the proof given in [Har77], Proposition II.3.2 -/
theorem isNoetherianRing_of_away : IsNoetherianRing R := by
  apply monotone_stabilizes_iff_noetherian.mp
  intro I
  let floc s := algebraMap R (Away (M := R) s)
  let suitableN s :=
    { n : â„• | âˆ€ m : â„•, n â‰¤ m â†’ (Ideal.map (floc s) (I n)) = (Ideal.map (floc s) (I m)) }
  let minN s := sInf (suitableN s)
  have hSuit : âˆ€ s : S, minN s âˆˆ suitableN s := by
    intro s
    apply Nat.sInf_mem
    let f : â„• â†’o Ideal (Away (M := R) s) :=
      âŸ¨fun n â†¦ Ideal.map (floc s) (I n), fun _ _ h â†¦ Ideal.map_mono (I.monotone h)âŸ©
    exact monotone_stabilizes_iff_noetherian.mpr (hN s) f
  let N := Finset.sup S minN
  use N
  have hN : âˆ€ s : S, minN s â‰¤ N := fun s => Finset.le_sup s.prop
  intro n hn
  rw [IsLocalization.ideal_eq_iInf_comap_map_away hS (I N),
      IsLocalization.ideal_eq_iInf_comap_map_away hS (I n),
      iInf_subtype', iInf_subtype']
  apply iInf_congr
  intro s
  congr 1
  rw [â† hSuit s N (hN s)]
  exact hSuit s n <| Nat.le_trans (hN s) hn

end localizationProps

variable {X : Scheme}

/-- If a scheme `X` has a cover by affine opens whose sections are Noetherian rings,
then `X` is locally Noetherian. -/
theorem isLocallyNoetherian_of_affine_cover {Î¹} {S : Î¹ â†’ X.affineOpens}
    (hS : (â¨† i, S i : X.Opens) = âŠ¤)
    (hS' : âˆ€ i, IsNoetherianRing Î“(X, S i)) : IsLocallyNoetherian X := by
  refine âŸ¨fun U => ?_âŸ©
  induction U using of_affine_open_cover S hS with
  | basicOpen U f hN =>
    have := U.prop.isLocalization_basicOpen f
    exact IsLocalization.isNoetherianRing (.powers f) Î“(X, X.basicOpen f) hN
  | openCover U s _ hN =>
    apply isNoetherianRing_of_away s â€¹_â€º
    intro âŸ¨f, hfâŸ©
    have : IsNoetherianRing Î“(X, X.basicOpen f) := hN âŸ¨f, hfâŸ©
    have := U.prop.isLocalization_basicOpen f
    have hEq := IsLocalization.algEquiv (.powers f) (Localization.Away f) Î“(X, X.basicOpen f)
    exact isNoetherianRing_of_ringEquiv Î“(X, X.basicOpen f) hEq.symm.toRingEquiv
  | hU => exact hS' _

/-- A scheme is locally Noetherian if and only if it is covered by affine opens whose sections
are Noetherian rings.

See [Har77], Proposition II.3.2. -/
theorem isLocallyNoetherian_iff_of_iSup_eq_top {Î¹} {S : Î¹ â†’ X.affineOpens}
    (hS : (â¨† i, S i : X.Opens) = âŠ¤) :
    IsLocallyNoetherian X â†” âˆ€ i, IsNoetherianRing Î“(X, S i) :=
  âŸ¨fun _ i => IsLocallyNoetherian.component_noetherian (S i),
   isLocallyNoetherian_of_affine_cover hSâŸ©

/-- A version of `isLocallyNoetherian_iff_of_iSup_eq_top` using `Scheme.OpenCover`. -/
theorem isLocallyNoetherian_iff_of_affine_openCover (ğ’° : Scheme.OpenCover.{v, u} X)
    [âˆ€ i, IsAffine (ğ’°.X i)] :
    IsLocallyNoetherian X â†” âˆ€ (i : ğ’°.Iâ‚€), IsNoetherianRing Î“(ğ’°.X i, âŠ¤) := by
  constructor
  Â· intro h i
    let U := Scheme.Hom.opensRange (ğ’°.f i)
    have := h.component_noetherian âŸ¨U, isAffineOpen_opensRange _âŸ©
    apply isNoetherianRing_of_ringEquiv (R := Î“(X, U))
    apply CategoryTheory.Iso.commRingCatIsoToRingEquiv
    exact (IsOpenImmersion.Î“IsoTop (ğ’°.f i)).symm
  Â· intro hCNoeth
    let fS i : X.affineOpens := âŸ¨Scheme.Hom.opensRange (ğ’°.f i), isAffineOpen_opensRange _âŸ©
    apply isLocallyNoetherian_of_affine_cover (S := fS)
    Â· rw [â† Scheme.OpenCover.iSup_opensRange ğ’°]
    intro i
    apply isNoetherianRing_of_ringEquiv (R := Î“(ğ’°.X i, âŠ¤))
    apply CategoryTheory.Iso.commRingCatIsoToRingEquiv
    exact IsOpenImmersion.Î“IsoTop (ğ’°.f i)

lemma isLocallyNoetherian_of_isOpenImmersion {Y : Scheme} (f : X âŸ¶ Y) [IsOpenImmersion f]
    [IsLocallyNoetherian Y] : IsLocallyNoetherian X := by
  refine âŸ¨fun U => ?_âŸ©
  let V : Y.affineOpens := âŸ¨f ''áµ U, IsAffineOpen.image_of_isOpenImmersion U.prop _âŸ©
  suffices Î“(X, U) â‰… Î“(Y, V) by
    convert isNoetherianRing_of_ringEquiv (R := Î“(Y, V)) _
    Â· apply CategoryTheory.Iso.commRingCatIsoToRingEquiv
      exact this.symm
    Â· exact IsLocallyNoetherian.component_noetherian V
  rw [â† Scheme.Hom.preimage_image_eq f U]
  trans
  Â· apply IsOpenImmersion.Î“Iso
  Â· suffices Scheme.Hom.opensRange f âŠ“ V = V by
      rw [this]
    rw [â† Opens.coe_inj]
    rw [Opens.coe_inf, Scheme.Hom.coe_opensRange, Set.inter_eq_right]
    exact Set.image_subset_range _ _

/-- If `ğ’°` is an open cover of a scheme `X`, then `X` is locally Noetherian if and only if
`ğ’°.X i` are all locally Noetherian. -/
theorem isLocallyNoetherian_iff_openCover (ğ’° : Scheme.OpenCover X) :
    IsLocallyNoetherian X â†” âˆ€ (i : ğ’°.Iâ‚€), IsLocallyNoetherian (ğ’°.X i) := by
  constructor
  Â· intro h i
    exact isLocallyNoetherian_of_isOpenImmersion (ğ’°.f i)
  Â· rw [isLocallyNoetherian_iff_of_affine_openCover (ğ’° := ğ’°.affineRefinement.openCover)]
    intro h i
    exact @isNoetherianRing_of_ringEquiv _ _ _ _
      (IsOpenImmersion.Î“IsoTop (PreZeroHypercover.f _ i.2)).symm.commRingCatIsoToRingEquiv
      (IsLocallyNoetherian.component_noetherian âŸ¨_, isAffineOpen_opensRange _âŸ©)

/-- If `R` is a Noetherian ring, `Spec R` is a Noetherian topological space. -/
instance {R : CommRingCat} [IsNoetherianRing R] :
    NoetherianSpace (Spec R) := by
  convert PrimeSpectrum.instNoetherianSpace (R := R)

lemma noetherianSpace_of_isAffine [IsAffine X] [IsNoetherianRing Î“(X, âŠ¤)] :
    NoetherianSpace X :=
  (noetherianSpace_iff_of_homeomorph X.isoSpec.inv.homeomorph).mp inferInstance

lemma noetherianSpace_of_isAffineOpen (U : X.Opens) (hU : IsAffineOpen U)
    [IsNoetherianRing Î“(X, U)] :
    NoetherianSpace U := by
  have : IsNoetherianRing Î“(U, âŠ¤) := isNoetherianRing_of_ringEquiv _
    (Scheme.restrictFunctorÎ“.app (op U)).symm.commRingCatIsoToRingEquiv
  exact @noetherianSpace_of_isAffine _ hU _

/-- Any open immersion `Z âŸ¶ X` with `X` locally Noetherian is quasi-compact. -/
@[stacks 01OX]
instance (priority := 100) {Z : Scheme} [IsLocallyNoetherian X]
    {f : Z âŸ¶ X} [IsOpenImmersion f] : QuasiCompact f := by
  apply quasiCompact_iff_forall_isAffineOpen.mpr
  intro U hU
  rw [Opens.map_coe, â† Set.preimage_inter_range]
  apply f.isOpenEmbedding.isInducing.isCompact_preimage'
  Â· apply (noetherianSpace_set_iff _).mp
    Â· convert noetherianSpace_of_isAffineOpen U hU
      apply IsLocallyNoetherian.component_noetherian âŸ¨U, hUâŸ©
    Â· exact Set.inter_subset_left
  Â· exact Set.inter_subset_right

/-- A locally Noetherian scheme is quasi-separated. -/
@[stacks 01OY]
instance (priority := 100) IsLocallyNoetherian.quasiSeparatedSpace [IsLocallyNoetherian X] :
    QuasiSeparatedSpace X := by
  apply quasiSeparatedSpace_iff_forall_affineOpens.mpr
  intro U V
  have hInd := U.2.fromSpec.isOpenEmbedding.isInducing
  apply (hInd.isCompact_preimage_iff ?_).mp
  Â· rw [â† Set.preimage_inter_range, IsAffineOpen.range_fromSpec, Set.inter_comm]
    apply hInd.isCompact_preimage'
    Â· apply (noetherianSpace_set_iff _).mp
      Â· convert noetherianSpace_of_isAffineOpen U.1 U.2
        apply IsLocallyNoetherian.component_noetherian
      Â· exact Set.inter_subset_left
    Â· rw [IsAffineOpen.range_fromSpec]
      exact Set.inter_subset_left
  Â· rw [IsAffineOpen.range_fromSpec]
    exact Set.inter_subset_left

/-- A scheme `X` is Noetherian if it is locally Noetherian and compact. -/
@[mk_iff]
class IsNoetherian (X : Scheme) : Prop extends IsLocallyNoetherian X, CompactSpace X

/-- A scheme is Noetherian if and only if it is covered by finitely many affine opens whose
sections are Noetherian rings. -/
theorem isNoetherian_iff_of_finite_iSup_eq_top {Î¹} [Finite Î¹] {S : Î¹ â†’ X.affineOpens}
    (hS : (â¨† i, S i : X.Opens) = âŠ¤) :
    IsNoetherian X â†” âˆ€ i, IsNoetherianRing Î“(X, S i) := by
  constructor
  Â· intro h i
    apply (isLocallyNoetherian_iff_of_iSup_eq_top hS).mp
    exact h.toIsLocallyNoetherian
  Â· intro h
    convert IsNoetherian.mk
    Â· exact isLocallyNoetherian_of_affine_cover hS h
    Â· constructor
      rw [â† Opens.coe_top, â† hS, Opens.iSup_mk]
      apply isCompact_iUnion
      intro i
      apply isCompact_iff_isCompact_univ.mpr
      convert CompactSpace.isCompact_univ
      have : NoetherianSpace (S i) := by
        apply noetherianSpace_of_isAffineOpen (S i).1 (S i).2
      apply NoetherianSpace.compactSpace (S i)

/-- A version of `isNoetherian_iff_of_finite_iSup_eq_top` using `Scheme.OpenCover`. -/
theorem isNoetherian_iff_of_finite_affine_openCover {ğ’° : Scheme.OpenCover.{v, u} X}
    [Finite ğ’°.Iâ‚€] [âˆ€ i, IsAffine (ğ’°.X i)] :
    IsNoetherian X â†” âˆ€ (i : ğ’°.Iâ‚€), IsNoetherianRing Î“(ğ’°.X i, âŠ¤) := by
  constructor
  Â· intro h i
    apply (isLocallyNoetherian_iff_of_affine_openCover _).mp
    exact h.toIsLocallyNoetherian
  Â· intro hNoeth
    convert IsNoetherian.mk
    Â· exact (isLocallyNoetherian_iff_of_affine_openCover _).mpr hNoeth
    Â· exact Scheme.OpenCover.compactSpace ğ’°

/-- A Noetherian scheme has a Noetherian underlying topological space. -/
@[stacks 01OZ]
instance (priority := 100) IsNoetherian.noetherianSpace [IsNoetherian X] :
    NoetherianSpace X := by
  apply TopologicalSpace.noetherian_univ_iff.mp
  let ğ’° := X.affineCover.finiteSubcover
  rw [â† ğ’°.iUnion_range]
  suffices âˆ€ i : ğ’°.Iâ‚€, NoetherianSpace (Set.range <| (ğ’°.f i)) by
    apply NoetherianSpace.iUnion
  intro i
  have : IsAffine (ğ’°.X i) := by
    rw [X.affineCover.finiteSubcover_X]
    apply Scheme.isAffine_affineCover
  let U : X.affineOpens := âŸ¨Scheme.Hom.opensRange (ğ’°.f i), isAffineOpen_opensRange _âŸ©
  convert noetherianSpace_of_isAffineOpen U.1 U.2
  apply IsLocallyNoetherian.component_noetherian

/-- Any morphism of schemes `f : X âŸ¶ Y` with `X` Noetherian is quasi-compact. -/
@[stacks 01P0]
instance (priority := 100) quasiCompact_of_noetherianSpace_source {X Y : Scheme}
    [NoetherianSpace X] (f : X âŸ¶ Y) : QuasiCompact f :=
  âŸ¨fun _ _ _ => NoetherianSpace.isCompact _âŸ©

/-- If `R` is a Noetherian ring, `Spec R` is a locally Noetherian scheme. -/
instance {R : CommRingCat} [IsNoetherianRing R] :
    IsLocallyNoetherian (Spec R) := by
  apply isLocallyNoetherian_of_affine_cover
    (Î¹ := Fin 1) (S := fun _ => âŸ¨âŠ¤, isAffineOpen_top (Spec R)âŸ©)
  Â· exact iSup_const
  Â· intro
    apply isNoetherianRing_of_ringEquiv R
    apply CategoryTheory.Iso.commRingCatIsoToRingEquiv
    exact (Scheme.Î“SpecIso R).symm

instance (priority := 100) {R : CommRingCat}
    [IsLocallyNoetherian (Spec R)] : IsNoetherianRing R := by
  have := IsLocallyNoetherian.component_noetherian âŸ¨âŠ¤, AlgebraicGeometry.isAffineOpen_top (Spec R)âŸ©
  apply isNoetherianRing_of_ringEquiv Î“(Spec R, âŠ¤)
  apply CategoryTheory.Iso.commRingCatIsoToRingEquiv
  exact Scheme.Î“SpecIso R

/-- If `R` is a Noetherian ring, `Spec R` is a Noetherian scheme. -/
instance {R : CommRingCat} [IsNoetherianRing R] :
    IsNoetherian (Spec R) where

instance {R} [CommRing R] [IsNoetherianRing R] :
    IsNoetherian <| Spec <| .of R := by
  suffices IsNoetherianRing (CommRingCat.of R) by infer_instance
  assumption

instance [IsLocallyNoetherian X] {x : X} : IsNoetherianRing (X.presheaf.stalk x) := by
  obtain âŸ¨U, hU, hU2, hU3âŸ© := exists_isAffineOpen_mem_and_subset (U := âŠ¤) (x := x) (by aesop)
  have := AlgebraicGeometry.IsAffineOpen.isLocalization_stalk hU âŸ¨x, hU2âŸ©
  exact @IsLocalization.isNoetherianRing _ _ (hU.primeIdealOf âŸ¨x, hU2âŸ©).asIdeal.primeCompl
        (X.presheaf.stalk x) _ (X.presheaf.algebra_section_stalk âŸ¨x, hU2âŸ©)
        this (IsLocallyNoetherian.component_noetherian âŸ¨U, hUâŸ©)

/-- `R` is a Noetherian ring if and only if `Spec R` is a Noetherian scheme. -/
theorem isNoetherian_Spec {R : CommRingCat} :
    IsNoetherian (Spec R) â†” IsNoetherianRing R :=
  âŸ¨fun _ => inferInstance,
   fun _ => inferInstanceâŸ©

/-- A Noetherian scheme has a finite number of irreducible components. -/
@[stacks 0BA8]
theorem finite_irreducibleComponents_of_isNoetherian [IsNoetherian X] :
    (irreducibleComponents X).Finite := NoetherianSpace.finite_irreducibleComponents

end AlgebraicGeometry
