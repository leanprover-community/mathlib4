/-
Copyright (c) 2024 Geno Racklin Asher. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Geno Racklin Asher
-/
import Mathlib.AlgebraicGeometry.Morphisms.QuasiSeparated
import Mathlib.AlgebraicGeometry.PrimeSpectrum.Noetherian
import Mathlib.RingTheory.Localization.Submodule

/-!
# Noetherian and Locally Noetherian Schemes

We introduce the concept of (locally) Noetherian schemes,
giving definitions, equivalent conditions, and basic properties.

## Main definitions

* `AlgebraicGeometry.IsLocallyNoetherian`: A scheme is locally Noetherian
  if the components of the structure sheaf at each affine open are Noetherian rings.

* `AlgebraicGeometry.IsNoetherian`: A scheme is Noetherian if it is locally Noetherian
  and quasi-compact as a topological space.

## Main results

* `AlgebraicGeometry.isLocallyNoetherian_iff_of_affine_openCover`: A scheme is locally Noetherian
  if and only if it is covered by affine opens whose sections are Noetherian rings.

* `AlgebraicGeometry.IsLocallyNoetherian.quasiSeparatedSpace`: A locally Noetherian scheme is
  quasi-separated.

* `AlgebraicGeometry.isNoetherian_iff_of_finite_affine_openCover`: A scheme is Noetherian
  if and only if it is covered by finitely many affine opens whose sections are Noetherian rings.

* `AlgebraicGeometry.IsNoetherian.noetherianSpace`: A Noetherian scheme is
  topologically a Noetherian space.

## References

* [Stacks: Noetherian Schemes](https://stacks.math.columbia.edu/tag/01OU)
* [Robin Hartshorne, *Algebraic Geometry*][Har77]

-/

universe u v

open Opposite AlgebraicGeometry Localization IsLocalization TopologicalSpace

namespace AlgebraicGeometry

/-- A scheme `X` is locally Noetherian if `ğ’ªâ‚“(U)` is Noetherian for all affine `U`. -/
class IsLocallyNoetherian (X : Scheme) : Prop where
  component_noetherian : âˆ€ (U : X.affineOpens),
    IsNoetherianRing Î“(X, U) := by infer_instance

section localizationProps

variable {R : Type u} [CommRing R] (S : Finset R) (hS : Ideal.span (Î± := R) S = âŠ¤)
  (hN : âˆ€ s : S, IsNoetherianRing (Away (M := R) s))

/-- Let `R` be a ring, and `f i` a finite collection of elements of `R` generating the unit ideal.
If the localization of `R` at each `f i` is noetherian, so is `R`.

We follow the proof given in [Har77], Proposition II.3.2 -/
theorem isNoetherianRing_of_away : IsNoetherianRing R := by
  apply Iff.mp
  apply monotone_stabilizes_iff_noetherian
  intro I
  let floc s := algebraMap R (Away (M := R) s)
  let suitableN s :=
    { n : â„• | âˆ€ m : â„•, n â‰¤ m â†’ (Ideal.map (floc s) (I n)) = (Ideal.map (floc s) (I m)) }
  let minN s := sInf (suitableN s)
  have hSuit : âˆ€ s : S, minN s âˆˆ suitableN s := by
    intro s
    apply Nat.sInf_mem
    let f : â„• â†’o Ideal (Away (M := R) s) :=
      âŸ¨fun n => Ideal.map (floc s) (I n), by
        intro n m hnm
        dsimp
        apply Ideal.map_mono
        exact I.monotone hnmâŸ©
    exact monotone_stabilizes_iff_noetherian.mpr (hN s) f
  let N := Finset.sup S minN
  use N
  have hN : âˆ€ s : S, minN s â‰¤ N := fun s => Finset.le_sup s.prop
  intro n hn
  rw [IsLocalization.ideal_eq_iInf_comap_map_away hS (I N),
      IsLocalization.ideal_eq_iInf_comap_map_away hS (I n),
      iInf_subtype', iInf_subtype']
  apply iInf_congr
  intro s
  congr 1
  rw [â† hSuit s N (hN s)]
  exact hSuit s n <| Nat.le_trans (hN s) hn

end localizationProps

variable {X : Scheme}

/-- If a scheme `X` has a cover by affine opens whose sections are Noetherian rings,
then `X` is locally Noetherian. -/
theorem isLocallyNoetherian_of_affine_cover {S : Set X.affineOpens}
    (hS : (â¨† i âˆˆ S, i : Opens X) = âŠ¤)
    (hS' : âˆ€ U âˆˆ S, IsNoetherianRing Î“(X, U)) : IsLocallyNoetherian X := by
  refine âŸ¨fun U => ?_âŸ©
  rw [iSup_subtype'] at hS
  apply of_affine_open_cover (P := _) (fun i : S => i) hS U _ _ (fun U => hS' U.1 U.2)
  Â· intro U f hN
    have := U.prop.isLocalization_basicOpen f
    exact IsLocalization.isNoetherianRing (.powers f) Î“(X, X.basicOpen f) hN
  Â· intro U s _ hN
    have : âˆ€ f : s, IsNoetherianRing (Away (M := Î“(X, U)) f) := by
      intro âŸ¨f, hfâŸ©
      have : IsNoetherianRing Î“(X, X.basicOpen f) := hN âŸ¨f, hfâŸ©
      apply isNoetherianRing_of_ringEquiv Î“(X, X.basicOpen f)
      have := U.prop.isLocalization_basicOpen f
      have hEq := IsLocalization.algEquiv (.powers f) (Localization.Away f) Î“(X, X.basicOpen f)
      exact hEq.symm.toRingEquiv
    apply isNoetherianRing_of_away
    assumption'

/-- A scheme is locally Noetherian if and only if it is covered by affine opens whose sections
are noetherian rings.

See [Har77], Proposition II.3.2. -/
theorem isLocallyNoetherian_iff_of_iSup_eq_top
    {S : Set X.affineOpens} (hS : (â¨† i âˆˆ S, i : Opens X) = âŠ¤) :
    IsLocallyNoetherian X â†” âˆ€ U âˆˆ S, IsNoetherianRing Î“(X, U) :=
  âŸ¨fun _ U _ => IsLocallyNoetherian.component_noetherian U,
   isLocallyNoetherian_of_affine_cover hSâŸ©

open CategoryTheory in
/-- A version of `isLocallyNoetherian_iff_of_iSup_eq_top` using `Scheme.OpenCover`. -/
theorem isLocallyNoetherian_iff_of_affine_openCover {ğ’° : Scheme.OpenCover.{v, u} X}
    (hAff : âˆ€ (i : ğ’°.J), IsAffine (ğ’°.obj i)) :
    IsLocallyNoetherian X â†”
    âˆ€ (i : ğ’°.J), IsNoetherianRing Î“(ğ’°.obj i, âŠ¤) := by
  constructor
  Â· intro h i
    let U := Scheme.Hom.opensRange (ğ’°.map i)
    have := h.component_noetherian âŸ¨U, isAffineOpen_opensRange _âŸ©
    apply isNoetherianRing_of_ringEquiv (R := Î“(X, U))
    apply CategoryTheory.Iso.commRingCatIsoToRingEquiv
    exact (Scheme.openImmersionÎ“ (ğ’°.map i)).symm
  Â· intro hCNoeth
    let fS i : X.affineOpens := âŸ¨Scheme.Hom.opensRange (ğ’°.map i), isAffineOpen_opensRange _âŸ©
    apply isLocallyNoetherian_of_affine_cover (S := Set.range fS)
    rw [â† Scheme.OpenCover.iSup_opensRange ğ’°, iSup_range]
    rintro _ âŸ¨i, rflâŸ©
    apply isNoetherianRing_of_ringEquiv (R := Î“(ğ’°.obj i, âŠ¤))
    apply CategoryTheory.Iso.commRingCatIsoToRingEquiv
    exact Scheme.openImmersionÎ“ (ğ’°.map i)


/-- A scheme is locally Noetherian if it has an open cover by
locally Noetherian schemes. -/
theorem isLocallyNoetherian_of_openCover (ğ’° : Scheme.OpenCover.{max v u, u} X)
    (hğ’° : âˆ€ (i : ğ’°.J), IsLocallyNoetherian (ğ’°.obj i)) :
    IsLocallyNoetherian X := by
  let ğ’°' := Scheme.OpenCover.affineRefinement ğ’°
  let m := Scheme.OpenCover.fromAffineRefinement ğ’°
  apply Iff.mpr
  apply isLocallyNoetherian_iff_of_affine_openCover (ğ’° := ğ’°'.openCover)
  Â· intro i
    rw [Scheme.AffineOpenCover.openCover_obj]
    exact isAffine_Spec _
  Â· intro i
    let X := (ğ’°.obj (m.idx i))
    let U : X.affineOpens := âŸ¨(m.app i).opensRange, by
      convert isAffineOpen_opensRange (m.app i)
      exact isAffine_Spec _âŸ©
    have hNoeth : IsNoetherianRing Î“(X, U) := by
      apply (hğ’° (m.idx i)).component_noetherian
    apply isNoetherianRing_of_ringEquiv (R := Î“(X, U))
    apply CategoryTheory.Iso.commRingCatIsoToRingEquiv
    symm
    exact Scheme.openImmersionÎ“ (m.app i)

/-- If `R` is a noetherian ring, `Spec R` is a noetherian topological space. -/
instance {R : CommRingCat} [IsNoetherianRing R] :
    NoetherianSpace (Spec R) := by
  convert PrimeSpectrum.instNoetherianSpace (R := R)

lemma noetherianSpace_of_isAffine [IsAffine X]
    (hX : IsNoetherianRing <| Î“(X, âŠ¤)) :
    NoetherianSpace X := by
  suffices f : Spec Î“(X, âŠ¤) â‰ƒâ‚œ X by
    apply (noetherianSpace_iff_of_homeomorph f).mp
    infer_instance
  apply TopCat.homeoOfIso
  exact CategoryTheory.asIso (Scheme.isoSpec X).symm.hom.val.base

lemma noetherianSpace_of_affineOpen (U : X.affineOpens)
    (hU : IsNoetherianRing Î“(X, U)) :
    NoetherianSpace U := by
  suffices h : IsNoetherianRing Î“(X âˆ£_áµ¤ â†‘U, âŠ¤) by
    exact noetherianSpace_of_isAffine h
  apply isNoetherianRing_of_ringEquiv (R := Î“(X, U))
  apply CategoryTheory.Iso.commRingCatIsoToRingEquiv
  exact ((Scheme.restrictFunctorÎ“ X).app (op U)).symm

/-- Any open immersion `Z âŸ¶ X` with `X` locally Noetherian is quasi-compact.

[Stacks: Lemma 01OX](https://stacks.math.columbia.edu/tag/01OX) -/
instance (priority := 100) {Z : Scheme} [IsLocallyNoetherian X]
    {f : Z âŸ¶ X} [h : IsOpenImmersion f] : QuasiCompact f := by
  apply (quasiCompact_iff_forall_affine f).mpr
  intro U hU
  rw [Opens.map_coe, â† Set.preimage_inter_range]
  apply Inducing.isCompact_preimage'
  Â· constructor
    exact h.base_open.induced
  Â· apply (noetherianSpace_set_iff _).mp
    apply noetherianSpace_of_affineOpen âŸ¨U, hUâŸ©
    apply IsLocallyNoetherian.component_noetherian
    exact Set.inter_subset_left
  Â· exact Set.inter_subset_right

/-- A locally Noetherian scheme is quasi-separated.

[Stacks: Lemma 01OY](https://stacks.math.columbia.edu/tag/01OY) -/
instance (priority := 100) IsLocallyNoetherian.quasiSeparatedSpace [IsLocallyNoetherian X] :
    QuasiSeparatedSpace X := by
  apply (quasiSeparatedSpace_iff_affine X).mpr
  intro U V
  let f := U.prop.fromSpec.val.base
  have hInd := (IsAffineOpen.isOpenImmersion_fromSpec U.prop).base_open.induced
  apply Iff.mp
  Â· apply Inducing.isCompact_preimage_iff (f := f)
    Â· constructor
      exact hInd
    Â· rw [IsAffineOpen.range_fromSpec]
      exact Set.inter_subset_left
  Â· rw [â† Set.preimage_inter_range, IsAffineOpen.range_fromSpec, Set.inter_comm]
    apply Inducing.isCompact_preimage'
    Â· constructor
      exact hInd
    Â· apply (noetherianSpace_set_iff _).mp
      apply noetherianSpace_of_affineOpen U
      apply IsLocallyNoetherian.component_noetherian
      exact Set.inter_subset_left
    Â· rw [IsAffineOpen.range_fromSpec]
      exact Set.inter_subset_left

/-- A scheme `X` is Noetherian if it is locally Noetherian and compact. -/
@[mk_iff]
class IsNoetherian (X : Scheme) extends IsLocallyNoetherian X, CompactSpace X : Prop

open Classical in
/-- A scheme is Noetherian if and only if it is covered by finitely many affine opens whose
sections are noetherian rings. -/
theorem isNoetherian_iff_of_finite_iSup_eq_top {S : Finset X.affineOpens}
    (hS : (â¨† i âˆˆ S, i : Opens X) = âŠ¤) :
    IsNoetherian X â†” âˆ€ U âˆˆ S, IsNoetherianRing Î“(X, U) := by
  constructor
  Â· intro h _ hU
    apply (isLocallyNoetherian_iff_of_iSup_eq_top hS).mp
    Â· exact h.toIsLocallyNoetherian
    Â· exact hU
  Â· intro h
    convert IsNoetherian.mk
    Â· exact isLocallyNoetherian_of_affine_cover hS h
    Â· constructor
      rw [â† Opens.coe_top, â† hS, iSup_subtype', Opens.iSup_mk]
      apply isCompact_iUnion
      intro U
      apply isCompact_iff_isCompact_univ.mpr
      have := noetherianSpace_of_affineOpen U.val (h U.1 U.2)
      apply CompactSpace.isCompact_univ

/-- A version of `isNoetherian_iff_of_finite_iSup_eq_top` using `Scheme.OpenCover`. -/
theorem isNoetherian_iff_of_finite_affine_openCover {ğ’° : Scheme.OpenCover.{v, u} X}
    (hFin: Finite ğ’°.J) (hAff : âˆ€ (i : ğ’°.J), IsAffine (ğ’°.obj i)) :
    IsNoetherian X â†” âˆ€ (i : ğ’°.J), IsNoetherianRing Î“(ğ’°.obj i, âŠ¤) := by
  constructor
  Â· intro h i
    apply (isLocallyNoetherian_iff_of_affine_openCover hAff).mp
    exact h.toIsLocallyNoetherian
  Â· intro hNoeth
    convert IsNoetherian.mk
    Â· exact (isLocallyNoetherian_iff_of_affine_openCover hAff).mpr hNoeth
    Â· exact Scheme.OpenCover.compactSpace ğ’°

open CategoryTheory in
/-- A Noetherian scheme has a Noetherian underlying topological space.

[Stacks, Lemma 01OZ](https://stacks.math.columbia.edu/tag/01OZ) -/
instance (priority := 100) IsNoetherian.noetherianSpace [h : IsNoetherian X] :
    NoetherianSpace X := by
  apply TopologicalSpace.noetherian_univ_iff.mp
  let ğ’° := X.affineCover.finiteSubcover
  rw [â† ğ’°.iUnion_range]
  suffices âˆ€ i : ğ’°.J, NoetherianSpace (Set.range <| (ğ’°.map i).val.base) by
    apply NoetherianSpace.iUnion
  intro i
  have : IsAffine (ğ’°.obj i) := by
    rw [X.affineCover.finiteSubcover_obj]
    apply Scheme.isAffine_affineCover
  let U : X.affineOpens := âŸ¨Scheme.Hom.opensRange (ğ’°.map i), isAffineOpen_opensRange _âŸ©
  apply noetherianSpace_of_affineOpen U
  apply h.component_noetherian

/-- Any morphism of schemes `f : X âŸ¶ Y` with `X` Noetherian is quasi-compact.

[Stacks, Lemma 01P0](https://stacks.math.columbia.edu/tag/01P0) -/
instance (priority := 100) quasiCompact_of_noetherianSpace_source {X Y : Scheme}
    [NoetherianSpace X] (f : X âŸ¶ Y) : QuasiCompact f :=
  âŸ¨fun _ _ _ => NoetherianSpace.isCompact _âŸ©

/-- If `R` is a Noetherian ring, `Spec R` is a locally Noetherian scheme. -/
instance {R : CommRingCat} [IsNoetherianRing R] :
    IsLocallyNoetherian (Spec R) := by
  apply isLocallyNoetherian_of_affine_cover (
    S := {âŸ¨âŠ¤, AlgebraicGeometry.isAffineOpen_top (Spec R)âŸ©})
  simp only [Set.mem_singleton_iff, Opens.iSup_mk, Opens.carrier_eq_coe, Set.iUnion_iUnion_eq_left,
    Opens.coe_top, Opens.mk_univ]
  rintro _ rfl
  apply isNoetherianRing_of_ringEquiv R
  apply CategoryTheory.Iso.commRingCatIsoToRingEquiv
  exact CategoryTheory.asIso (toSpecÎ“ R)

instance (priority := 100) {R : CommRingCat}
    [h : IsLocallyNoetherian <| Spec R] : IsNoetherianRing R := by
  have := h.component_noetherian âŸ¨âŠ¤, AlgebraicGeometry.isAffineOpen_top (Spec R)âŸ©
  apply isNoetherianRing_of_ringEquiv Î“(Spec R, âŠ¤)
  apply CategoryTheory.Iso.commRingCatIsoToRingEquiv
  exact (CategoryTheory.asIso (toSpecÎ“ R)).symm

/-- If `R` is a Noetherian ring, `Spec R` is a Noetherian scheme. -/
instance {R : CommRingCat} [IsNoetherianRing R] :
    IsNoetherian (Spec R) where


/-- `R` is a Noetherian ring if and only if `Spec R` is a Noetherian scheme. -/
theorem isNoetherian_Spec {R : CommRingCat} :
    IsNoetherian (Spec R) â†” IsNoetherianRing R :=
  âŸ¨fun _ => by infer_instance,
   fun _ => by infer_instanceâŸ©

/-- A Noetherian scheme has a finite number of irreducible components.

[Stacks, Lemma 0BA8](https://stacks.math.columbia.edu/tag/0BA8) -/
theorem finite_irreducibleComponents_of_isNoetherian [IsNoetherian X] :
    (irreducibleComponents X).Finite := NoetherianSpace.finite_irreducibleComponents
