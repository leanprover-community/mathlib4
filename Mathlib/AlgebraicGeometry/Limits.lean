/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Pullbacks
import Mathlib.AlgebraicGeometry.AffineScheme

/-!
# (Co)Limits of Schemes

We construct various limits and colimits in the category of schemes.

* The existence of fibred products was shown in `Mathlib/AlgebraicGeometry/Pullbacks.lean`.
* `Spec ‚Ñ§` is the terminal object.
* The preceding two results imply that `Scheme` has all finite limits.
* The empty scheme is the (strict) initial object.
* The disjoint union is the coproduct of a family of schemes, and the forgetful functor to
  `LocallyRingedSpace` and `TopCat` preserves them.

## TODO

* Spec preserves finite coproducts.

-/

suppress_compilation


universe u v

open CategoryTheory CategoryTheory.Limits Opposite TopologicalSpace

attribute [local instance] Opposite.small

namespace AlgebraicGeometry

/-- `Spec ‚Ñ§` is the terminal object in the category of schemes. -/
noncomputable def specZIsTerminal : IsTerminal Spec(‚Ñ§) :=
  @IsTerminal.isTerminalObj _ _ _ _ Scheme.Spec _ inferInstance
    (terminalOpOfInitial CommRingCat.zIsInitial)

/-- `Spec ‚Ñ§` is the terminal object in the category of schemes. -/
noncomputable def specULiftZIsTerminal : IsTerminal Spec(ULift.{u} ‚Ñ§) :=
  @IsTerminal.isTerminalObj _ _ _ _ Scheme.Spec _ inferInstance
    (terminalOpOfInitial CommRingCat.isInitial)

instance : HasTerminal Scheme :=
  hasTerminal_of_hasTerminal_of_preservesLimit Scheme.Spec

instance : IsAffine (‚ä§_ Scheme.{u}) :=
  .of_isIso (PreservesTerminal.iso Scheme.Spec).inv

instance : HasFiniteLimits Scheme :=
  hasFiniteLimits_of_hasTerminal_and_pullbacks

instance (X : Scheme.{u}) : X.Over (‚ä§_ _) := ‚ü®terminal.from _‚ü©
instance {X Y : Scheme.{u}} [X.Over (‚ä§_ Scheme)] [Y.Over (‚ä§_ Scheme)] (f : X ‚ü∂ Y) :
    @Scheme.Hom.IsOver _ _ f (‚ä§_ Scheme) ‚Äπ_‚Ä∫ ‚Äπ_‚Ä∫ := ‚ü®Subsingleton.elim _ _‚ü©

instance {X : Scheme} : Subsingleton (X.Over (‚ä§_ Scheme)) :=
  ‚ü®fun ‚ü®a‚ü© ‚ü®b‚ü© ‚Ü¶ by simp [Subsingleton.elim a b]‚ü©

section Initial

/-- The map from the empty scheme. -/
@[simps]
def Scheme.emptyTo (X : Scheme.{u}) : ‚àÖ ‚ü∂ X :=
  ‚ü®{  base := TopCat.ofHom ‚ü®fun x => PEmpty.elim x, by fun_prop‚ü©
      c := { app := fun _ => CommRingCat.punitIsTerminal.from _ } }, fun x => PEmpty.elim x‚ü©

@[ext]
theorem Scheme.empty_ext {X : Scheme.{u}} (f g : ‚àÖ ‚ü∂ X) : f = g :=
  Scheme.Hom.ext' (Subsingleton.elim (Œ± := ‚àÖ ‚ü∂ _) _ _)

theorem Scheme.eq_emptyTo {X : Scheme.{u}} (f : ‚àÖ ‚ü∂ X) : f = Scheme.emptyTo X :=
  Scheme.empty_ext f (Scheme.emptyTo X)

instance Scheme.hom_unique_of_empty_source (X : Scheme.{u}) : Unique (‚àÖ ‚ü∂ X) :=
  ‚ü®‚ü®Scheme.emptyTo _‚ü©, fun _ => Scheme.empty_ext _ _‚ü©

/-- The empty scheme is the initial object in the category of schemes. -/
def emptyIsInitial : IsInitial (‚àÖ : Scheme.{u}) :=
  IsInitial.ofUnique _

@[simp]
theorem emptyIsInitial_to : emptyIsInitial.to = Scheme.emptyTo :=
  rfl

instance : IsEmpty (‚àÖ : Scheme.{u}) :=
  show IsEmpty PEmpty by infer_instance

instance spec_punit_isEmpty : IsEmpty Spec(PUnit.{u+1}) :=
  inferInstanceAs <| IsEmpty (PrimeSpectrum PUnit)

instance (priority := 100) isOpenImmersion_of_isEmpty {X Y : Scheme} (f : X ‚ü∂ Y)
    [IsEmpty X] : IsOpenImmersion f := by
  apply (config := { allowSynthFailures := true }) IsOpenImmersion.of_stalk_iso
  ¬∑ exact .of_isEmpty (X := X) _
  ¬∑ intro (i : X); exact isEmptyElim i

instance (priority := 100) isIso_of_isEmpty {X Y : Scheme} (f : X ‚ü∂ Y) [IsEmpty Y] :
    IsIso f := by
  haveI : IsEmpty X := f.base.hom.1.isEmpty
  have : Epi f.base := by
    rw [TopCat.epi_iff_surjective]; rintro (x : Y)
    exact isEmptyElim x
  apply IsOpenImmersion.to_iso

/-- A scheme is initial if its underlying space is empty . -/
noncomputable def isInitialOfIsEmpty {X : Scheme} [IsEmpty X] : IsInitial X :=
  emptyIsInitial.ofIso (asIso <| emptyIsInitial.to _)

/-- `Spec 0` is the initial object in the category of schemes. -/
noncomputable def specPunitIsInitial : IsInitial Spec(PUnit.{u+1}) :=
  emptyIsInitial.ofIso (asIso <| emptyIsInitial.to _)

instance (priority := 100) isAffine_of_isEmpty {X : Scheme} [IsEmpty X] : IsAffine X :=
  .of_isIso (inv (emptyIsInitial.to X) ‚â´ emptyIsInitial.to Spec(PUnit))

instance : HasInitial Scheme.{u} :=
  hasInitial_of_unique ‚àÖ

instance initial_isEmpty : IsEmpty (‚ä•_ Scheme) :=
  ‚ü®fun x => ((initial.to Scheme.empty :).base x).elim‚ü©

theorem isAffineOpen_bot (X : Scheme) : IsAffineOpen (‚ä• : X.Opens) :=
  @isAffine_of_isEmpty _ (inferInstanceAs (IsEmpty (‚àÖ : Set X)))

instance : HasStrictInitialObjects Scheme :=
  hasStrictInitialObjects_of_initial_is_strict fun A f => by infer_instance

instance {X : Scheme} [IsEmpty X] (U : X.Opens) : Subsingleton Œì(X, U) := by
  obtain rfl : U = ‚ä• := Subsingleton.elim _ _; infer_instance

-- This is also true for schemes with two points.
-- But there are non-affine schemes with three points.
instance (priority := low) {X : Scheme.{u}} [Subsingleton X] : IsAffine X := by
  cases isEmpty_or_nonempty X with
  | inl h => infer_instance
  | inr h =>
  obtain ‚ü®x‚ü© := h
  obtain ‚ü®_, ‚ü®U, hU : IsAffine _, rfl‚ü©, hxU, -‚ü© :=
    (isBasis_affine_open X).exists_subset_of_mem_open (a := x) (by trivial) isOpen_univ
  obtain rfl : U = ‚ä§ := by ext y; simpa [Subsingleton.elim y x]
  exact .of_isIso (Scheme.topIso X).inv

end Initial

section Coproduct

variable {Œπ : Type u} (f : Œπ ‚Üí Scheme.{u})

variable {œÉ : Type v} (g : œÉ ‚Üí Scheme.{u})

noncomputable
instance [Small.{u} œÉ] :
    CreatesColimitsOfShape (Discrete œÉ) Scheme.forgetToLocallyRingedSpace.{u} where

instance [Small.{u} œÉ] : PreservesColimitsOfShape (Discrete œÉ) Scheme.forgetToTop.{u} :=
  inferInstanceAs (PreservesColimitsOfShape (Discrete œÉ) (Scheme.forgetToLocallyRingedSpace ‚ãô
      LocallyRingedSpace.forgetToSheafedSpace ‚ãô SheafedSpace.forget CommRingCat))

instance [Small.{u} œÉ] : HasColimitsOfShape (Discrete œÉ) Scheme.{u} :=
  ‚ü®fun _ ‚Ü¶ hasColimit_of_created _ Scheme.forgetToLocallyRingedSpace‚ü©

lemma sigmaŒπ_eq_iff (i j : Œπ) (x y) :
    (Sigma.Œπ f i).base x = (Sigma.Œπ f j).base y ‚Üî
      (Sigma.mk i x : Œ£ i, f i) = Sigma.mk j y := by
  refine (Scheme.IsLocallyDirected.Œπ_eq_Œπ_iff _).trans ‚ü®?_, ?_‚ü©
  ¬∑ rintro ‚ü®k, ‚ü®‚ü®‚ü®‚ü©‚ü©‚ü©, ‚ü®‚ü®‚ü®‚ü©‚ü©‚ü©, x, rfl, rfl‚ü©; simp
  ¬∑ simp only [Discrete.functor_obj_eq_as, Sigma.mk.injEq]
    rintro ‚ü®rfl, e‚ü©
    obtain rfl := (heq_eq_eq x y).mp e
    exact ‚ü®‚ü®i‚ü©, ùüô _, ùüô _, x, by simp‚ü©

/-- The images of each component in the coproduct is disjoint. -/
lemma disjoint_opensRange_sigmaŒπ (i j : Œπ) (h : i ‚â† j) :
    Disjoint (Sigma.Œπ f i).opensRange (Sigma.Œπ f j).opensRange := by
  intro U hU hU' x hx
  obtain ‚ü®x, rfl‚ü© := hU hx
  obtain ‚ü®y, hy‚ü© := hU' hx
  obtain ‚ü®rfl‚ü© := (sigmaŒπ_eq_iff _ _ _ _ _).mp hy
  cases h rfl

/-- The cover of `‚àê X` by the `X·µ¢`. -/
@[simps!]
noncomputable def sigmaOpenCover [Small.{u} œÉ] : (‚àê g).OpenCover :=
  (Scheme.IsLocallyDirected.openCover (Discrete.functor g)).copy œÉ g (Sigma.Œπ _)
  (discreteEquiv.symm) (fun _ ‚Ü¶ Iso.refl _) (fun _ ‚Ü¶ rfl)

/-- The underlying topological space of the coproduct is homeomorphic to the disjoint union. -/
noncomputable
def sigmaMk : (Œ£ i, f i) ‚âÉ‚Çú (‚àê f :) :=
  TopCat.homeoOfIso ((colimit.isoColimitCocone ‚ü®_, TopCat.sigmaCofanIsColimit _‚ü©).symm ‚â™‚â´
    (PreservesCoproduct.iso Scheme.forgetToTop f).symm)

@[simp]
lemma sigmaMk_mk (i) (x : f i) :
    sigmaMk f (.mk i x) = (Sigma.Œπ f i).base x := by
  change ((TopCat.sigmaCofan (fun x ‚Ü¶ (f x).toTopCat)).inj i ‚â´
    (colimit.isoColimitCocone ‚ü®_, TopCat.sigmaCofanIsColimit _‚ü©).inv ‚â´ _) x =
      Scheme.forgetToTop.map (Sigma.Œπ f i) x
  congr 2
  refine (colimit.isoColimitCocone_Œπ_inv_assoc ‚ü®_, TopCat.sigmaCofanIsColimit _‚ü© _ _).trans ?_
  exact Œπ_comp_sigmaComparison Scheme.forgetToTop _ _

open scoped Function in
private lemma isOpenImmersion_sigmaDesc_aux
    {X : Scheme.{u}} (Œ± : ‚àÄ i, f i ‚ü∂ X) [‚àÄ i, IsOpenImmersion (Œ± i)]
    (hŒ± : Pairwise (Disjoint on (Set.range <| Œ± ¬∑ |>.base))) :
    IsOpenImmersion (Sigma.desc Œ±) := by
  rw [IsOpenImmersion.iff_stalk_iso]
  constructor
  ¬∑ suffices Topology.IsOpenEmbedding ((Sigma.desc Œ±).base ‚àò sigmaMk f) by
      convert this.comp (sigmaMk f).symm.isOpenEmbedding; ext; simp
    refine .of_continuous_injective_isOpenMap ?_ ?_ ?_
    ¬∑ fun_prop
    ¬∑ rintro ‚ü®ix, x‚ü© ‚ü®iy, y‚ü© e
      have : (Œ± ix).base x = (Œ± iy).base y := by
        simpa [‚Üê Scheme.comp_base_apply] using e
      obtain rfl : ix = iy := by
        by_contra h
        exact Set.disjoint_iff_forall_ne.mp (hŒ± h) ‚ü®x, rfl‚ü© ‚ü®y, this.symm‚ü© rfl
      rw [(Œ± ix).isOpenEmbedding.injective this]
    ¬∑ rw [isOpenMap_sigma]
      intro i
      simpa [‚Üê Scheme.comp_base_apply] using (Œ± i).isOpenEmbedding.isOpenMap
  ¬∑ intro x
    have ‚ü®y, hy‚ü© := (Scheme.IsLocallyDirected.openCover (Discrete.functor f)).covers x
    rw [‚Üê hy]
    refine IsIso.of_isIso_fac_right
      (g := ((Scheme.IsLocallyDirected.openCover (Discrete.functor f)).map _).stalkMap y)
      (h := (X.presheaf.stalkCongr (.of_eq ?_)).hom ‚â´ (Œ± _).stalkMap _) ?_
    ¬∑ simp [‚Üê Scheme.comp_base_apply]
    ¬∑ simp [‚Üê Scheme.stalkMap_comp, Scheme.stalkMap_congr_hom _ _ (colimit.Œπ_desc _ _)]

open scoped Function in
lemma isOpenImmersion_sigmaDesc [Small.{u} œÉ]
    {X : Scheme.{u}} (Œ± : ‚àÄ i, g i ‚ü∂ X) [‚àÄ i, IsOpenImmersion (Œ± i)]
    (hŒ± : Pairwise (Disjoint on (Set.range <| Œ± ¬∑ |>.base))) :
    IsOpenImmersion (Sigma.desc Œ±) := by
  obtain ‚ü®Œπ, ‚ü®e‚ü©‚ü© := Small.equiv_small (Œ± := œÉ)
  convert IsOpenImmersion.comp ((Sigma.reindex e.symm g).inv) (Sigma.desc fun i ‚Ü¶ Œ± _)
  ¬∑ refine Sigma.hom_ext _ _ fun i ‚Ü¶ ?_
    obtain ‚ü®i, rfl‚ü© := e.symm.surjective i
    simp
  ¬∑ apply isOpenImmersion_sigmaDesc_aux
    intro i j hij
    exact hŒ± (fun h ‚Ü¶ hij (e.symm.injective h))

open scoped Function in
/-- `S` is the disjoint union of `X·µ¢` if the `X·µ¢` are covering, pairwise disjoint open subschemes
of `S`. -/
lemma nonempty_isColimit_cofanMk_of [Small.{u} œÉ]
    {X : œÉ ‚Üí Scheme.{u}} {S : Scheme.{u}} (f : ‚àÄ i, X i ‚ü∂ S) [‚àÄ i, IsOpenImmersion (f i)]
    (hcov : ‚®Ü i, (f i).opensRange = ‚ä§) (hdisj : Pairwise (Disjoint on (f ¬∑ |>.opensRange))) :
    Nonempty (IsColimit <| Cofan.mk S f) := by
  have : IsOpenImmersion (Sigma.desc f) := by
    refine isOpenImmersion_sigmaDesc _ _ (fun i j hij ‚Ü¶ ?_)
    simpa [Function.onFun_apply, disjoint_iff, Opens.ext_iff] using hdisj hij
  simp only [‚Üê Cofan.isColimit_iff_isIso_sigmaDesc (Cofan.mk S f), cofan_mk_inj, Cofan.mk_pt]
  apply isIso_of_isOpenImmersion_of_opensRange_eq_top
  rw [eq_top_iff]
  intro x hx
  have : x ‚àà ‚®Ü i, (f i).opensRange := by rwa [hcov]
  obtain ‚ü®i, y, rfl‚ü© := by simpa only [Opens.iSup_mk, Opens.mem_mk, Set.mem_iUnion] using this
  use Sigma.Œπ X i |>.base y
  simp [‚Üê Scheme.comp_base_apply]

variable (X Y : Scheme.{u})

/-- (Implementation Detail)
The coproduct of the two schemes is given by indexed coproducts over `WalkingPair`. -/
noncomputable
def coprodIsoSigma : X ‚®ø Y ‚âÖ ‚àê fun i : ULift.{u} WalkingPair ‚Ü¶ i.1.casesOn X Y :=
  Sigma.whiskerEquiv Equiv.ulift.symm (fun _ ‚Ü¶ by exact Iso.refl _)

lemma Œπ_left_coprodIsoSigma_inv : Sigma.Œπ _ ‚ü®.left‚ü© ‚â´ (coprodIsoSigma X Y).inv = coprod.inl :=
  Sigma.Œπ_comp_map' _ _ _

lemma Œπ_right_coprodIsoSigma_inv : Sigma.Œπ _ ‚ü®.right‚ü© ‚â´ (coprodIsoSigma X Y).inv = coprod.inr :=
  Sigma.Œπ_comp_map' _ _ _

instance : IsOpenImmersion (coprod.inl : X ‚ü∂ X ‚®ø Y) := by
  rw [‚Üê Œπ_left_coprodIsoSigma_inv]; infer_instance

instance : IsOpenImmersion (coprod.inr : Y ‚ü∂ X ‚®ø Y) := by
  rw [‚Üê Œπ_right_coprodIsoSigma_inv]; infer_instance

lemma isCompl_range_inl_inr :
    IsCompl (Set.range (coprod.inl : X ‚ü∂ X ‚®ø Y).base)
      (Set.range (coprod.inr : Y ‚ü∂ X ‚®ø Y).base) :=
  ((TopCat.binaryCofan_isColimit_iff _).mp
    ‚ü®mapIsColimitOfPreservesOfIsColimit Scheme.forgetToTop.{u} _ _ (coprodIsCoprod X Y)‚ü©).2.2

lemma isCompl_opensRange_inl_inr :
    IsCompl (coprod.inl : X ‚ü∂ X ‚®ø Y).opensRange (coprod.inr : Y ‚ü∂ X ‚®ø Y).opensRange := by
  convert isCompl_range_inl_inr X Y
  simp only [isCompl_iff, disjoint_iff, codisjoint_iff, ‚Üê TopologicalSpace.Opens.coe_inj]
  rfl

/-- The underlying topological space of the coproduct is homeomorphic to the disjoint union -/
noncomputable
def coprodMk : X ‚äï Y ‚âÉ‚Çú (X ‚®ø Y : Scheme.{u}) :=
  TopCat.homeoOfIso ((colimit.isoColimitCocone ‚ü®_, TopCat.binaryCofanIsColimit _ _‚ü©).symm ‚â™‚â´
    PreservesColimitPair.iso Scheme.forgetToTop X Y)

@[simp]
lemma coprodMk_inl (x : X) :
    coprodMk X Y (.inl x) = (coprod.inl : X ‚ü∂ X ‚®ø Y).base x := by
  change ((TopCat.binaryCofan X Y).inl ‚â´
    (colimit.isoColimitCocone ‚ü®_, TopCat.binaryCofanIsColimit _ _‚ü©).inv ‚â´ _) x =
      Scheme.forgetToTop.map coprod.inl x
  congr 2
  refine (colimit.isoColimitCocone_Œπ_inv_assoc ‚ü®_, TopCat.binaryCofanIsColimit _ _‚ü© _ _).trans ?_
  exact coprodComparison_inl Scheme.forgetToTop

@[simp]
lemma coprodMk_inr (x : Y) :
    coprodMk X Y (.inr x) = (coprod.inr : Y ‚ü∂ X ‚®ø Y).base x := by
  change ((TopCat.binaryCofan X Y).inr ‚â´
    (colimit.isoColimitCocone ‚ü®_, TopCat.binaryCofanIsColimit _ _‚ü©).inv ‚â´ _) x =
      Scheme.forgetToTop.map coprod.inr x
  congr 2
  refine (colimit.isoColimitCocone_Œπ_inv_assoc ‚ü®_, TopCat.binaryCofanIsColimit _ _‚ü© _ _).trans ?_
  exact coprodComparison_inr Scheme.forgetToTop

/-- The open cover of the coproduct of two schemes. -/
noncomputable
def coprodOpenCover.{w} : (X ‚®ø Y).OpenCover where
  J := PUnit.{w + 1} ‚äï PUnit.{w + 1}
  obj x := x.elim (fun _ ‚Ü¶ X) (fun _ ‚Ü¶ Y)
  map x := x.rec (fun _ ‚Ü¶ coprod.inl) (fun _ ‚Ü¶ coprod.inr)
  f x := ((coprodMk X Y).symm x).elim (fun _ ‚Ü¶ Sum.inl .unit) (fun _ ‚Ü¶ Sum.inr .unit)
  covers x := by
    obtain ‚ü®x, rfl‚ü© := (coprodMk X Y).surjective x
    simp only [Sum.elim_inl, Sum.elim_inr, Set.mem_range]
    rw [Homeomorph.symm_apply_apply]
    obtain (x | x) := x
    ¬∑ simp only [Sum.elim_inl, coprodMk_inl, exists_apply_eq_apply]
    ¬∑ simp only [Sum.elim_inr, coprodMk_inr, exists_apply_eq_apply]
  map_prop x := x.rec (fun _ ‚Ü¶ inferInstance) (fun _ ‚Ü¶ inferInstance)

/-- If `X` and `Y` are open disjoint and covering open subschemes of `S`,
`S` is the disjoint union of `X` and `Y`. -/
lemma nonempty_isColimit_binaryCofanMk_of_isCompl {X Y S : Scheme.{u}}
    (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [IsOpenImmersion f] [IsOpenImmersion g]
    (hf : IsCompl f.opensRange g.opensRange) :
    Nonempty (IsColimit <| BinaryCofan.mk f g) := by
  let c' : Cofan fun j ‚Ü¶ (WalkingPair.casesOn j X Y : Scheme.{u}) :=
    .mk S fun j ‚Ü¶ WalkingPair.casesOn j f g
  let i : BinaryCofan.mk f g ‚âÖ c' := Cofan.ext (Iso.refl _) (by rintro (b|b) <;> rfl)
  refine ‚ü®IsColimit.ofIsoColimit (Nonempty.some ?_) i.symm‚ü©
  let fi (j : WalkingPair) : WalkingPair.casesOn j X Y ‚ü∂ S := WalkingPair.casesOn j f g
  convert nonempty_isColimit_cofanMk_of fi _ _
  ¬∑ intro i
    cases i <;> (simp [fi]; infer_instance)
  ¬∑ simpa [‚Üê WalkingPair.equivBool.symm.iSup_comp, iSup_bool_eq, ‚Üê codisjoint_iff] using hf.2
  ¬∑ intro i j hij
    match i, j with
    | .left, .right => simpa [fi] using hf.1
    | .right, .left => simpa [fi] using hf.1.symm

variable (R S : Type u) [CommRing R] [CommRing S]

/-- The map `Spec R ‚®ø Spec S ‚ü∂ Spec (R √ó S)`.
This is an isomorphism as witnessed by an `IsIso` instance provided below. -/
noncomputable
def coprodSpec : Spec(R) ‚®ø Spec(S) ‚ü∂ Spec(R √ó S) :=
  coprod.desc (Spec.map (CommRingCat.ofHom <| RingHom.fst _ _))
    (Spec.map (CommRingCat.ofHom <| RingHom.snd _ _))

@[simp, reassoc]
lemma coprodSpec_inl : coprod.inl ‚â´ coprodSpec R S =
    Spec.map (CommRingCat.ofHom <| RingHom.fst R S) :=
  coprod.inl_desc _ _

@[simp, reassoc]
lemma coprodSpec_inr : coprod.inr ‚â´ coprodSpec R S =
    Spec.map (CommRingCat.ofHom <| RingHom.snd R S) :=
  coprod.inr_desc _ _

lemma coprodSpec_coprodMk (x) :
    (coprodSpec R S).base (coprodMk _ _ x) = (PrimeSpectrum.primeSpectrumProd R S).symm x := by
  apply PrimeSpectrum.ext
  obtain (x | x) := x <;>
    simp only [coprodMk_inl, coprodMk_inr, ‚Üê Scheme.comp_base_apply,
      coprodSpec, coprod.inl_desc, coprod.inr_desc]
  ¬∑ change Ideal.comap _ _ = x.asIdeal.prod ‚ä§
    ext; simp [Ideal.prod, CommRingCat.ofHom]
  ¬∑ change Ideal.comap _ _ = Ideal.prod ‚ä§ x.asIdeal
    ext; simp [Ideal.prod, CommRingCat.ofHom]

lemma coprodSpec_apply (x) :
    (coprodSpec R S).base x =
      (PrimeSpectrum.primeSpectrumProd R S).symm ((coprodMk Spec(R) Spec(S)).symm x) := by
  rw [‚Üê coprodSpec_coprodMk, Homeomorph.apply_symm_apply]

lemma isIso_stalkMap_coprodSpec (x) :
    IsIso ((coprodSpec R S).stalkMap x) := by
  obtain ‚ü®x | x, rfl‚ü© := (coprodMk _ _).surjective x
  ¬∑ have := Scheme.stalkMap_comp coprod.inl (coprodSpec R S) x
    rw [‚Üê IsIso.comp_inv_eq, Scheme.stalkMap_congr_hom _ (Spec.map _) (coprodSpec_inl R S)] at this
    rw [coprodMk_inl, ‚Üê this]
    letI := (RingHom.fst R S).toAlgebra
    have := IsLocalization.away_fst (R := R) (S := S)
    have : IsOpenImmersion (Spec.map (CommRingCat.ofHom (RingHom.fst R S))) :=
      IsOpenImmersion.of_isLocalization (1, 0)
    infer_instance
  ¬∑ have := Scheme.stalkMap_comp coprod.inr (coprodSpec R S) x
    rw [‚Üê IsIso.comp_inv_eq, Scheme.stalkMap_congr_hom _ (Spec.map _) (coprodSpec_inr R S)] at this
    rw [coprodMk_inr, ‚Üê this]
    letI := (RingHom.snd R S).toAlgebra
    have := IsLocalization.away_snd (R := R) (S := S)
    have : IsOpenImmersion (Spec.map (CommRingCat.ofHom (RingHom.snd R S))) :=
      IsOpenImmersion.of_isLocalization (0, 1)
    infer_instance

instance : IsIso (coprodSpec R S) := by
  rw [isIso_iff_stalk_iso]
  refine ‚ü®?_, isIso_stalkMap_coprodSpec R S‚ü©
  convert_to IsIso (TopCat.isoOfHomeo (X := Spec(R √ó S)) <|
    PrimeSpectrum.primeSpectrumProdHomeo.trans (coprodMk Spec(R) Spec(S))).inv
  ¬∑ ext x; exact coprodSpec_apply R S x
  ¬∑ infer_instance

instance (R S : CommRingCat.{u}·µí·µñ) : IsIso (coprodComparison Scheme.Spec R S) := by
  obtain ‚ü®R‚ü© := R; obtain ‚ü®S‚ü© := S
  have : coprodComparison Scheme.Spec (.op R) (.op S) ‚â´ (Spec.map
    ((limit.isoLimitCone ‚ü®_, CommRingCat.prodFanIsLimit R S‚ü©).inv ‚â´
      (opProdIsoCoprod R S).unop.inv)) = coprodSpec R S := by
    ext1
    ¬∑ rw [coprodComparison_inl_assoc, coprodSpec, coprod.inl_desc, Scheme.Spec_map,
        ‚Üê Spec.map_comp, Category.assoc, Iso.unop_inv, opProdIsoCoprod_inv_inl,
        limit.isoLimitCone_inv_œÄ]
      rfl
    ¬∑ rw [coprodComparison_inr_assoc, coprodSpec, coprod.inr_desc, Scheme.Spec_map,
        ‚Üê Spec.map_comp, Category.assoc, Iso.unop_inv, opProdIsoCoprod_inv_inr,
        limit.isoLimitCone_inv_œÄ]
      rfl
  rw [(IsIso.eq_comp_inv _).mpr this]
  infer_instance

instance : PreservesColimitsOfShape (Discrete WalkingPair) Scheme.Spec.{u} :=
  ‚ü®fun {_} ‚Ü¶
    have (X Y : CommRingCat.{u}·µí·µñ) := PreservesColimitPair.of_iso_coprod_comparison Scheme.Spec X Y
    preservesColimit_of_iso_diagram _ (diagramIsoPair _).symm‚ü©

instance : PreservesColimitsOfShape (Discrete PEmpty.{1}) Scheme.Spec.{u} := by
  have : IsEmpty (Scheme.Spec.obj (‚ä•_ CommRingCat·µí·µñ)) :=
    @Function.isEmpty _ _ spec_punit_isEmpty (Scheme.Spec.mapIso
      (initialIsoIsInitial (initialOpOfTerminal CommRingCat.punitIsTerminal))).hom.base
  have := preservesInitial_of_iso Scheme.Spec (asIso (initial.to _))
  exact preservesColimitsOfShape_pempty_of_preservesInitial _

instance {J : Type*} [Finite J] : PreservesColimitsOfShape (Discrete J) Scheme.Spec.{u} :=
  preservesFiniteCoproductsOfPreservesBinaryAndInitial _ _

/-- The canonical map `‚àê Spec R·µ¢ ‚ü∂ Spec (Œ† R·µ¢)`.
This is an isomorphism when the product is finite. -/
noncomputable
def sigmaSpec (R : Œπ ‚Üí CommRingCat) : (‚àê fun i ‚Ü¶ Spec (R i)) ‚ü∂ Spec(Œ† i, R i) :=
  Sigma.desc (fun i ‚Ü¶ Spec.map (CommRingCat.ofHom (Pi.evalRingHom _ i)))

@[reassoc (attr := simp)]
lemma Œπ_sigmaSpec (R : Œπ ‚Üí CommRingCat) (i) :
    Sigma.Œπ _ i ‚â´ sigmaSpec R = Spec.map (CommRingCat.ofHom (Pi.evalRingHom _ i)) :=
  Sigma.Œπ_desc _ _

instance (i) (R : Œπ ‚Üí Type _) [‚àÄ i, CommRing (R i)] :
    IsOpenImmersion (Spec.map (CommRingCat.ofHom (Pi.evalRingHom (R ¬∑) i))) := by
  classical
  letI := (Pi.evalRingHom R i).toAlgebra
  have : IsLocalization.Away (Function.update (Œ≤ := R) 0 i 1) (R i) := by
    apply IsLocalization.away_of_isIdempotentElem_of_mul
    ¬∑ ext j; by_cases h : j = i <;> aesop
    ¬∑ intro x y
      constructor
      ¬∑ intro e; ext j; by_cases h : j = i <;> aesop
      ¬∑ intro e; simpa using congr_fun e i
    ¬∑ exact Function.surjective_eval _
  exact IsOpenImmersion.of_isLocalization (Function.update 0 i 1)

instance (R : Œπ ‚Üí CommRingCat.{u}) : IsOpenImmersion (sigmaSpec R) := by
  classical
  apply isOpenImmersion_sigmaDesc
  intro ix iy h
  refine Set.disjoint_iff_forall_ne.mpr ?_
  rintro _ ‚ü®x, rfl‚ü© _ ‚ü®y, rfl‚ü© e
  have : DFinsupp.single (Œ≤ := (R ¬∑)) iy 1 iy ‚àà y.asIdeal :=
    (PrimeSpectrum.ext_iff.mp e).le (x := DFinsupp.single iy 1)
      (show DFinsupp.single (Œ≤ := (R ¬∑)) iy 1 ix ‚àà x.asIdeal by simp [h.symm])
  simp [‚Üê Ideal.eq_top_iff_one, y.2.ne_top] at this

instance [Finite Œπ] (R : Œπ ‚Üí CommRingCat.{u}) : IsIso (sigmaSpec R) := by
  have : sigmaSpec R =
      (colimit.isoColimitCocone ‚ü®_,
        (IsColimit.precomposeHomEquiv Discrete.natIsoFunctor.symm _).symm (isColimitOfPreserves
          Scheme.Spec (Fan.IsLimit.op (CommRingCat.piFanIsLimit R)))‚ü©).hom := by
    ext1
    simp; rfl
  rw [this]
  infer_instance

instance [Finite Œπ] [‚àÄ i, IsAffine (f i)] : IsAffine (‚àê f) :=
  .of_isIso ((Sigma.mapIso (fun i ‚Ü¶ (f i).isoSpec)).hom ‚â´ sigmaSpec _)

instance [IsAffine X] [IsAffine Y] : IsAffine (X ‚®ø Y) :=
  .of_isIso ((coprod.mapIso X.isoSpec Y.isoSpec).hom ‚â´ coprodSpec _ _)

end Coproduct

instance : CartesianMonoidalCategory Scheme := .ofHasFiniteProducts
instance : BraidedCategory Scheme := .ofCartesianMonoidalCategory

end AlgebraicGeometry
