/-
Copyright (c) 2024 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Stalk
import Mathlib.Geometry.RingedSpace.LocallyRingedSpace.ResidueField

/-!

# Residue fields of points

## Main definitions

The following are in the `AlgebraicGeometry.Scheme` namespace:

- `AlgebraicGeometry.Scheme.residueField`: The residue field of the stalk at `x`.
- `AlgebraicGeometry.Scheme.evaluation`: For open subsets `U` of `X` containing `x`,
  the evaluation map from sections over `U` to the residue field at `x`.
- `AlgebraicGeometry.Scheme.Hom.residueFieldMap`: A morphism of schemes induce a homomorphism of
  residue fields.
- `AlgebraicGeometry.Scheme.fromSpecResidueField`: The canonical map `Spec Œ∫(x) ‚ü∂ X`.
- `AlgebraicGeometry.Scheme.SpecToEquivOfField`: morphisms `Spec K ‚ü∂ X` for a field `K` correspond
  to pairs of `x : X` with embedding `Œ∫(x) ‚ü∂ K`.


-/

universe u

open CategoryTheory TopologicalSpace Opposite IsLocalRing

noncomputable section

namespace AlgebraicGeometry.Scheme

variable (X : Scheme.{u}) {U : X.Opens}

/-- The residue field of `X` at a point `x` is the residue field of the stalk of `X`
at `x`. -/
def residueField (x : X) : CommRingCat :=
  CommRingCat.of <| IsLocalRing.ResidueField (X.presheaf.stalk x)

instance (x : X) : Field (X.residueField x) :=
  inferInstanceAs <| Field (IsLocalRing.ResidueField (X.presheaf.stalk x))

instance (x : X) : Unique (Spec (X.residueField x)) :=
  inferInstanceAs (Unique (Spec (CommRingCat.of _)))

/-- The residue map from the stalk to the residue field. -/
def residue (X : Scheme.{u}) (x) : X.presheaf.stalk x ‚ü∂ X.residueField x :=
  CommRingCat.ofHom (IsLocalRing.residue (X.presheaf.stalk x))

/-- See `AlgebraicGeometry.IsClosedImmersion.Spec_map_residue` for the stronger result that
`Spec.map (X.residue x)` is a closed immersion. -/
instance {X : Scheme.{u}} (x) : IsPreimmersion (Spec.map (X.residue x)) :=
  IsPreimmersion.mk_Spec_map
    (PrimeSpectrum.isClosedEmbedding_comap_of_surjective _ _
      Ideal.Quotient.mk_surjective).isEmbedding
    (RingHom.surjectiveOnStalks_of_surjective (Ideal.Quotient.mk_surjective))

@[simp]
lemma Spec_map_residue_apply {X : Scheme.{u}} (x : X) (s : Spec (X.residueField x)) :
    (Spec.map (X.residue x)).base s = closedPoint (X.presheaf.stalk x) :=
  IsLocalRing.PrimeSpectrum.comap_residue _ s

lemma residue_surjective (X : Scheme.{u}) (x) : Function.Surjective (X.residue x) :=
  Ideal.Quotient.mk_surjective

instance (X : Scheme.{u}) (x) : Epi (X.residue x) :=
  ConcreteCategory.epi_of_surjective _ (X.residue_surjective x)

/-- If `K` is a field and `f : ùí™_{X, x} ‚ü∂ K` is a ring map, then this is the induced
map `Œ∫(x) ‚ü∂ K`. -/
def descResidueField {K : Type u} [Field K] {X : Scheme.{u}} {x : X}
    (f : X.presheaf.stalk x ‚ü∂ .of K) [IsLocalHom f.hom] :
    X.residueField x ‚ü∂ .of K :=
  CommRingCat.ofHom (IsLocalRing.ResidueField.lift (S := K) f.hom)

@[reassoc (attr := simp)]
lemma residue_descResidueField {K : Type u} [Field K] {X : Scheme.{u}} {x}
    (f : X.presheaf.stalk x ‚ü∂ .of K) [IsLocalHom f.hom] :
    X.residue x ‚â´ X.descResidueField f = f :=
  CommRingCat.hom_ext <| RingHom.ext fun _ ‚Ü¶ rfl

/--
If `U` is an open of `X` containing `x`, we have a canonical ring map from the sections
over `U` to the residue field of `x`.

If we interpret sections over `U` as functions of `X` defined on `U`, then this ring map
corresponds to evaluation at `x`.
-/
def evaluation (U : X.Opens) (x : X) (hx : x ‚àà U) : Œì(X, U) ‚ü∂ X.residueField x :=
  X.presheaf.germ U x hx ‚â´ X.residue _

@[reassoc]
lemma germ_residue (x hx) : X.presheaf.germ U x hx ‚â´ X.residue x = X.evaluation U x hx := rfl

/-- The global evaluation map from `Œì(X, ‚ä§)` to the residue field at `x`. -/
abbrev Œìevaluation (x : X) : Œì(X, ‚ä§) ‚ü∂ X.residueField x :=
  X.evaluation ‚ä§ x trivial

@[simp]
lemma evaluation_eq_zero_iff_notMem_basicOpen (x : X) (hx : x ‚àà U) (f : Œì(X, U)) :
    X.evaluation U x hx f = 0 ‚Üî x ‚àâ X.basicOpen f :=
  X.toLocallyRingedSpace.evaluation_eq_zero_iff_notMem_basicOpen ‚ü®x, hx‚ü© f

@[deprecated (since := "2025-05-23")]
alias evaluation_eq_zero_iff_not_mem_basicOpen := evaluation_eq_zero_iff_notMem_basicOpen

lemma evaluation_ne_zero_iff_mem_basicOpen (x : X) (hx : x ‚àà U) (f : Œì(X, U)) :
    X.evaluation U x hx f ‚â† 0 ‚Üî x ‚àà X.basicOpen f := by
  simp

lemma basicOpen_eq_bot_iff_forall_evaluation_eq_zero (f : X.presheaf.obj (op U)) :
    X.basicOpen f = ‚ä• ‚Üî ‚àÄ (x : U), X.evaluation U x x.property f = 0 :=
  X.toLocallyRingedSpace.basicOpen_eq_bot_iff_forall_evaluation_eq_zero f

variable {X Y : Scheme.{u}} (f : X ‚ü∂ Y)

/-- If `X ‚ü∂ Y` is a morphism of locally ringed spaces and `x` a point of `X`, we obtain
a morphism of residue fields in the other direction. -/
def Hom.residueFieldMap (f : X.Hom Y) (x : X) :
    Y.residueField (f.base x) ‚ü∂ X.residueField x :=
  CommRingCat.ofHom <| IsLocalRing.ResidueField.map (f.stalkMap x).hom

@[reassoc]
lemma residue_residueFieldMap (x : X) :
    Y.residue (f.base x) ‚â´ f.residueFieldMap x = f.stalkMap x ‚â´ X.residue x := by
  simp [Hom.residueFieldMap]
  rfl

@[simp]
lemma residueFieldMap_id (x : X) :
    Hom.residueFieldMap (ùüô X) x = ùüô (X.residueField x) :=
  LocallyRingedSpace.residueFieldMap_id _

@[simp]
lemma residueFieldMap_comp {Z : Scheme.{u}} (g : Y ‚ü∂ Z) (x : X) :
    (f ‚â´ g).residueFieldMap x = g.residueFieldMap (f.base x) ‚â´ f.residueFieldMap x :=
  LocallyRingedSpace.residueFieldMap_comp _ _ _

@[reassoc]
lemma evaluation_naturality {V : Opens Y} (x : X) (hx : f.base x ‚àà V) :
    Y.evaluation V (f.base x) hx ‚â´ f.residueFieldMap x =
      f.app V ‚â´ X.evaluation (f ‚Åª¬π·µÅ V) x hx :=
  LocallyRingedSpace.evaluation_naturality f.1 ‚ü®x, hx‚ü©

lemma evaluation_naturality_apply {V : Opens Y} (x : X) (hx : f.base x ‚àà V) (s) :
    f.residueFieldMap x (Y.evaluation V (f.base x) hx s) =
      X.evaluation (f ‚Åª¬π·µÅ V) x hx (f.app V s) :=
  LocallyRingedSpace.evaluation_naturality_apply f.1 ‚ü®x, hx‚ü© s

@[reassoc]
lemma Œìevaluation_naturality (x : X) :
    Y.Œìevaluation (f.base x) ‚â´ f.residueFieldMap x =
      f.c.app (op ‚ä§) ‚â´ X.Œìevaluation x :=
  LocallyRingedSpace.Œìevaluation_naturality f.toLRSHom x

lemma Œìevaluation_naturality_apply (x : X) (a : Y.presheaf.obj (op ‚ä§)) :
    f.residueFieldMap x (Y.Œìevaluation (f.base x) a) =
      X.Œìevaluation x (f.c.app (op ‚ä§) a) :=
  LocallyRingedSpace.Œìevaluation_naturality_apply f.toLRSHom x a

instance [IsOpenImmersion f] (x) : IsIso (f.residueFieldMap x) :=
  (IsLocalRing.ResidueField.mapEquiv
    (asIso (f.stalkMap x)).commRingCatIsoToRingEquiv).toCommRingCatIso.isIso_hom

section congr

-- replace this def if hard to work with
/-- The isomorphism between residue fields of equal points. -/
def residueFieldCongr {x y : X} (h : x = y) :
    X.residueField x ‚âÖ X.residueField y :=
  eqToIso (by subst h; rfl)

@[simp]
lemma residueFieldCongr_refl {x : X} :
    X.residueFieldCongr (refl x) = Iso.refl _ := rfl

@[simp]
lemma residueFieldCongr_symm {x y : X} (e : x = y) :
    (X.residueFieldCongr e).symm = X.residueFieldCongr e.symm := rfl

@[simp]
lemma residueFieldCongr_inv {x y : X} (e : x = y) :
    (X.residueFieldCongr e).inv = (X.residueFieldCongr e.symm).hom := rfl

@[simp]
lemma residueFieldCongr_trans {x y z : X} (e : x = y) (e' : y = z) :
    X.residueFieldCongr e ‚â™‚â´ X.residueFieldCongr e' = X.residueFieldCongr (e.trans e') := by
  subst e e'
  rfl

@[reassoc (attr := simp)]
lemma residueFieldCongr_trans_hom (X : Scheme) {x y z : X} (e : x = y) (e' : y = z) :
    (X.residueFieldCongr e).hom ‚â´ (X.residueFieldCongr e').hom =
      (X.residueFieldCongr (e.trans e')).hom := by
  subst e e'
  rfl

@[reassoc]
lemma residue_residueFieldCongr (X : Scheme) {x y : X} (h : x = y) :
    X.residue x ‚â´ (X.residueFieldCongr h).hom =
      (X.presheaf.stalkCongr (.of_eq h)).hom ‚â´ X.residue y := by
  subst h
  simp

lemma Hom.residueFieldMap_congr {f g : X ‚ü∂ Y} (e : f = g) (x : X) :
    f.residueFieldMap x = (Y.residueFieldCongr (by subst e; rfl)).hom ‚â´ g.residueFieldMap x := by
  subst e; simp

end congr

section fromResidueField

/-- The canonical map `Spec Œ∫(x) ‚ü∂ X`. -/
def fromSpecResidueField (X : Scheme) (x : X) :
    Spec (X.residueField x) ‚ü∂ X :=
  Spec.map (X.residue x) ‚â´ X.fromSpecStalk x

instance {X : Scheme.{u}} (x : X) : IsPreimmersion (X.fromSpecResidueField x) := by
  dsimp only [Scheme.fromSpecResidueField]
  rw [IsPreimmersion.comp_iff]
  infer_instance

@[simps] noncomputable
instance (x : X) : (Spec (X.residueField x)).Over X := ‚ü®X.fromSpecResidueField x‚ü©

noncomputable
instance (x : X) : (Spec (X.residueField x)).CanonicallyOver X where

@[reassoc (attr := simp)]
lemma residueFieldCongr_fromSpecResidueField {x y : X} (h : x = y) :
    Spec.map (X.residueFieldCongr h).hom ‚â´ X.fromSpecResidueField _ =
      X.fromSpecResidueField _ := by
  subst h; simp

instance {x y : X} (h : x = y) : (Spec.map (X.residueFieldCongr h).hom).IsOver X where

@[reassoc (attr := simp)]
lemma Hom.Spec_map_residueFieldMap_fromSpecResidueField (x : X) :
    Spec.map (f.residueFieldMap x) ‚â´ Y.fromSpecResidueField _ =
      X.fromSpecResidueField x ‚â´ f := by
  dsimp only [fromSpecResidueField]
  rw [Category.assoc, ‚Üê Spec_map_stalkMap_fromSpecStalk, ‚Üê Spec.map_comp_assoc,
    ‚Üê Spec.map_comp_assoc]
  rfl

instance [X.Over Y] (x : X) : Spec.map ((X ‚Üò Y).residueFieldMap x) |>.IsOver Y where

@[simp]
lemma fromSpecResidueField_apply (x : X.carrier) (s : Spec (X.residueField x)) :
    (X.fromSpecResidueField x).base s = x := by
  simp [fromSpecResidueField]

lemma range_fromSpecResidueField (x : X.carrier) :
    Set.range (X.fromSpecResidueField x).base = {x} := by
  ext s
  simp only [Set.mem_range, fromSpecResidueField_apply, Set.mem_singleton_iff, eq_comm (a := s)]
  constructor
  ¬∑ rintro ‚ü®-, h‚ü©
    exact h
  ¬∑ rintro rfl
    exact ‚ü®closedPoint (X.residueField x), rfl‚ü©

lemma descResidueField_fromSpecResidueField {K : Type*} [Field K] (X : Scheme) {x}
    (f : X.presheaf.stalk x ‚ü∂ .of K) [IsLocalHom f.hom] :
    Spec.map (X.descResidueField f) ‚â´
      X.fromSpecResidueField x = Spec.map f ‚â´ X.fromSpecStalk x := by
  simp [fromSpecResidueField, ‚Üê Spec.map_comp_assoc]

lemma descResidueField_stalkClosedPointTo_fromSpecResidueField
    (K : Type u) [Field K] (X : Scheme.{u}) (f : Spec (.of K) ‚ü∂ X) :
    Spec.map (@descResidueField (CommRingCat.of K) _ X _ (Scheme.stalkClosedPointTo f)
        _) ‚â´
      X.fromSpecResidueField (f.base (closedPoint K)) = f := by
  rw [X.descResidueField_fromSpecResidueField]
  rw [Scheme.Spec_stalkClosedPointTo_fromSpecStalk]

end fromResidueField

/-- A helper lemma to work with `AlgebraicGeometry.Scheme.SpecToEquivOfField`. -/
lemma SpecToEquivOfField_eq_iff {K : Type*} [Field K] {X : Scheme}
    {f‚ÇÅ f‚ÇÇ : Œ£ x : X.carrier, X.residueField x ‚ü∂ .of K} :
    f‚ÇÅ = f‚ÇÇ ‚Üî ‚àÉ e : f‚ÇÅ.1 = f‚ÇÇ.1, f‚ÇÅ.2 = (X.residueFieldCongr e).hom ‚â´ f‚ÇÇ.2 := by
  constructor
  ¬∑ rintro rfl
    simp
  ¬∑ obtain ‚ü®f, _‚ü© := f‚ÇÅ
    obtain ‚ü®g, _‚ü© := f‚ÇÇ
    rintro ‚ü®(rfl : f = g), h‚ü©
    simpa

/-- For a field `K` and a scheme `X`, the morphisms `Spec K ‚ü∂ X` bijectively correspond
to pairs of points `x` of `X` and embeddings `Œ∫(x) ‚ü∂ K`. -/
def SpecToEquivOfField (K : Type u) [Field K] (X : Scheme.{u}) :
    (Spec (.of K) ‚ü∂ X) ‚âÉ Œ£ x, X.residueField x ‚ü∂ .of K where
  toFun f :=
    ‚ü®_, X.descResidueField (Scheme.stalkClosedPointTo f)‚ü©
  invFun xf := Spec.map xf.2 ‚â´ X.fromSpecResidueField xf.1
  left_inv := Scheme.descResidueField_stalkClosedPointTo_fromSpecResidueField K X
  right_inv f := by
    rw [SpecToEquivOfField_eq_iff]
    simp only [CommRingCat.coe_of, Scheme.comp_coeBase, TopCat.coe_comp, Function.comp_apply,
      Scheme.fromSpecResidueField_apply, exists_true_left]
    rw [‚Üê Spec.map_inj, Spec.map_comp, ‚Üê cancel_mono (X.fromSpecResidueField _)]
    erw [Scheme.descResidueField_stalkClosedPointTo_fromSpecResidueField]
    simp

end Scheme

end AlgebraicGeometry
