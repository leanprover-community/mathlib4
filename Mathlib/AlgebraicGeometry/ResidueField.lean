/-
Copyright (c) 2024 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Geometry.RingedSpace.LocallyRingedSpace.ResidueField
import Mathlib.AlgebraicGeometry.Stalk

/-!

# Residue fields of points

## Main definitions

The following are in the `AlgebraicGeometry.Scheme` namespace:

- `AlgebraicGeometry.Scheme.residueField`: The residue field of the stalk at `x`.
- `AlgebraicGeometry.Scheme.evaluation`: For open subsets `U` of `X` containing `x`,
  the evaluation map from sections over `U` to the residue field at `x`.
- `AlgebraicGeometry.Scheme.Hom.residueFieldMap`: A morphism of schemes induce a homomorphism of
  residue fields.
- `AlgebraicGeometry.Scheme.fromSpecResidueField`: The canonical map `Spec Œ∫(x) ‚ü∂ X`.

-/

universe u

open CategoryTheory TopologicalSpace Opposite

noncomputable section

namespace AlgebraicGeometry.Scheme

variable (X : Scheme.{u}) {U : X.Opens}

/-- The residue field of `X` at a point `x` is the residue field of the stalk of `X`
at `x`. -/
def residueField (x : X) : CommRingCat :=
  CommRingCat.of <| LocalRing.ResidueField (X.presheaf.stalk x)

instance (x : X) : Field (X.residueField x) :=
  inferInstanceAs <| Field (LocalRing.ResidueField (X.presheaf.stalk x))

/-- The residue map from the stalk to the residue field. -/
def residue (X : Scheme.{u}) (x) : X.presheaf.stalk x ‚ü∂ X.residueField x :=
  LocalRing.residue _

lemma residue_surjective (X : Scheme.{u}) (x) : Function.Surjective (X.residue x) :=
  Ideal.Quotient.mk_surjective

instance (X : Scheme.{u}) (x) : Epi (X.residue x) :=
  ConcreteCategory.epi_of_surjective _ (X.residue_surjective x)

/--
If `U` is an open of `X` containing `x`, we have a canonical ring map from the sections
over `U` to the residue field of `x`.

If we interpret sections over `U` as functions of `X` defined on `U`, then this ring map
corresponds to evaluation at `x`.
-/
def evaluation (U : X.Opens) (x : X) (hx : x ‚àà U) : Œì(X, U) ‚ü∂ X.residueField x :=
  X.presheaf.germ U x hx ‚â´ X.residue _

@[reassoc]
lemma germ_residue (x hx) : X.presheaf.germ U x hx ‚â´ X.residue x = X.evaluation U x hx := rfl

/-- The global evaluation map from `Œì(X, ‚ä§)` to the residue field at `x`. -/
abbrev Œìevaluation (x : X) : Œì(X, ‚ä§) ‚ü∂ X.residueField x :=
  X.evaluation ‚ä§ x trivial

@[simp]
lemma evaluation_eq_zero_iff_not_mem_basicOpen (x : X) (hx : x ‚àà U) (f : Œì(X, U)) :
    X.evaluation U x hx f = 0 ‚Üî x ‚àâ X.basicOpen f :=
  X.toLocallyRingedSpace.evaluation_eq_zero_iff_not_mem_basicOpen ‚ü®x, hx‚ü© f

lemma evaluation_ne_zero_iff_mem_basicOpen (x : X) (hx : x ‚àà U) (f : Œì(X, U)) :
    X.evaluation U x hx f ‚â† 0 ‚Üî x ‚àà X.basicOpen f := by
  simp

lemma basicOpen_eq_bot_iff_forall_evaluation_eq_zero (f : X.presheaf.obj (op U)) :
    X.basicOpen f = ‚ä• ‚Üî ‚àÄ (x : U), X.evaluation U x x.property f = 0 :=
  X.toLocallyRingedSpace.basicOpen_eq_bot_iff_forall_evaluation_eq_zero f

variable {X Y : Scheme.{u}} (f : X ‚ü∂ Y)


-- TODO: This instance is found before #6045.
-- We need this strange instance for `residueFieldMap`, the type of `F` must be fixed
-- like this. The instance `IsLocalRingHom (f.stalkMap x)` already exists, but does not work for
-- `residueFieldMap`.
instance (x): IsLocalRingHom (F := Y.presheaf.stalk (f.base x) ‚Üí+* X.presheaf.stalk x)
    (f.stalkMap x) :=
  f.1.2 x

/-- If `X ‚ü∂ Y` is a morphism of locally ringed spaces and `x` a point of `X`, we obtain
a morphism of residue fields in the other direction. -/
def Hom.residueFieldMap (f : X.Hom Y) (x : X) :
    Y.residueField (f.base x) ‚ü∂ X.residueField x :=
  LocalRing.ResidueField.map (f.stalkMap x)

@[reassoc]
lemma residue_residueFieldMap (x : X) :
    Y.residue (f.base x) ‚â´ f.residueFieldMap x = f.stalkMap x ‚â´ X.residue x := by
  simp [Hom.residueFieldMap]
  rfl

@[simp]
lemma residueFieldMap_id (x : X) :
    Hom.residueFieldMap (ùüô X) x = ùüô (X.residueField x) :=
  LocallyRingedSpace.residueFieldMap_id _

@[simp]
lemma residueFieldMap_comp {Z : Scheme.{u}} (g : Y ‚ü∂ Z) (x : X) :
    (f ‚â´ g).residueFieldMap x = g.residueFieldMap (f.base x) ‚â´ f.residueFieldMap x :=
  LocallyRingedSpace.residueFieldMap_comp _ _ _

@[reassoc]
lemma evaluation_naturality {V : Opens Y} (x : X) (hx : f.base x ‚àà V) :
    Y.evaluation V (f.base x) hx ‚â´ f.residueFieldMap x =
      f.app V ‚â´ X.evaluation (f ‚Åª¬π·µÅ V) x hx :=
  LocallyRingedSpace.evaluation_naturality f.1 ‚ü®x, hx‚ü©

lemma evaluation_naturality_apply {V : Opens Y} (x : X) (hx : f.base x ‚àà V) (s) :
    f.residueFieldMap x (Y.evaluation V (f.base x) hx s) =
      X.evaluation (f ‚Åª¬π·µÅ V) x hx (f.app V s) :=
  LocallyRingedSpace.evaluation_naturality_apply f.1 ‚ü®x, hx‚ü© s

@[reassoc]
lemma Œìevaluation_naturality (x : X) :
    Y.Œìevaluation (f.base x) ‚â´ f.residueFieldMap x =
      f.c.app (op ‚ä§) ‚â´ X.Œìevaluation x :=
  LocallyRingedSpace.Œìevaluation_naturality f.toLRSHom x

lemma Œìevaluation_naturality_apply (x : X) (a : Y.presheaf.obj (op ‚ä§)) :
    f.residueFieldMap x (Y.Œìevaluation (f.base x) a) =
      X.Œìevaluation x (f.c.app (op ‚ä§) a) :=
  LocallyRingedSpace.Œìevaluation_naturality_apply f.toLRSHom x a

instance [IsOpenImmersion f] (x) : IsIso (f.residueFieldMap x) :=
  (LocalRing.ResidueField.mapEquiv
    (asIso (f.stalkMap x)).commRingCatIsoToRingEquiv).toCommRingCatIso.isIso_hom

section congr

-- replace this def if hard to work with
/-- The isomorphism between residue fields of equal points. -/
def residueFieldCongr {x y : X} (h : x = y) :
    X.residueField x ‚âÖ X.residueField y :=
  eqToIso (by subst h; rfl)

@[simp]
lemma residueFieldCongr_refl {x : X} :
    X.residueFieldCongr (refl x) = Iso.refl _ := rfl

@[simp]
lemma residueFieldCongr_symm {x y : X} (e : x = y) :
    (X.residueFieldCongr e).symm = X.residueFieldCongr e.symm := rfl

@[simp]
lemma residueFieldCongr_inv {x y : X} (e : x = y) :
    (X.residueFieldCongr e).inv = (X.residueFieldCongr e.symm).hom := rfl

@[simp]
lemma residueFieldCongr_trans {x y z : X} (e : x = y) (e' : y = z) :
    X.residueFieldCongr e ‚â™‚â´ X.residueFieldCongr e' = X.residueFieldCongr (e.trans e') := by
  subst e e'
  rfl

@[reassoc (attr := simp)]
lemma residueFieldCongr_trans_hom (X : Scheme) {x y z : X} (e : x = y) (e' : y = z) :
    (X.residueFieldCongr e).hom ‚â´ (X.residueFieldCongr e').hom =
      (X.residueFieldCongr (e.trans e')).hom := by
  subst e e'
  rfl

@[reassoc]
lemma residue_residueFieldCongr (X : Scheme) {x y : X} (h : x = y) :
    X.residue x ‚â´ (X.residueFieldCongr h).hom =
      (X.presheaf.stalkCongr (.of_eq h)).hom ‚â´ X.residue y := by
  subst h
  simp

end congr

section fromResidueField

/-- The canonical map `Spec Œ∫(x) ‚ü∂ X`. -/
def fromSpecResidueField (X : Scheme) (x : X) :
    Spec (X.residueField x) ‚ü∂ X :=
  Spec.map (CommRingCat.ofHom (X.residue x)) ‚â´ X.fromSpecStalk x

@[reassoc (attr := simp)]
lemma residueFieldCongr_fromSpecResidueField {x y : X} (h : x = y) :
    Spec.map (X.residueFieldCongr h).hom ‚â´ X.fromSpecResidueField _ =
      X.fromSpecResidueField _ := by
  subst h; simp

end fromResidueField

end Scheme

end AlgebraicGeometry
