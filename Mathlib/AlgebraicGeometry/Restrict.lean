/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Cover.Open

/-!
# Restriction of Schemes and Morphisms

## Main definition
- `AlgebraicGeometry.Scheme.restrict`: The restriction of a scheme along an open embedding.
  The map `X.restrict f ‚ü∂ X` is `AlgebraicGeometry.Scheme.ofRestrict`.
  `U : X.Opens` has a coercion to `Scheme` and `U.Œπ` is a shorthand
  for `X.restrict U.open_embedding : U ‚ü∂ X`.
- `AlgebraicGeometry.morphism_restrict`: The restriction of `X ‚ü∂ Y` to `X ‚à£_·µ§ f ‚Åª¬π·µÅ U ‚ü∂ Y ‚à£_·µ§ U`.

-/

-- Explicit universe annotations were used in this file to improve performance #12737


noncomputable section

open TopologicalSpace CategoryTheory Opposite

open CategoryTheory.Limits

namespace AlgebraicGeometry

universe v v‚ÇÅ v‚ÇÇ u u‚ÇÅ

variable {C : Type u‚ÇÅ} [Category.{v} C]

section

variable {X : Scheme.{u}} (U : X.Opens)

namespace Scheme.Opens

/-- Open subset of a scheme as a scheme. -/
@[coe]
def toScheme {X : Scheme.{u}} (U : X.Opens) : Scheme.{u} :=
  X.restrict U.openEmbedding

instance : CoeOut X.Opens Scheme := ‚ü®toScheme‚ü©

/-- The restriction of a scheme to an open subset. -/
@[simps! val_base_apply]
def Œπ : ‚ÜëU ‚ü∂ X := X.ofRestrict _

instance : IsOpenImmersion U.Œπ := inferInstanceAs (IsOpenImmersion (X.ofRestrict _))

lemma toScheme_carrier : (U : Type u) = (U : Set X) := rfl

lemma toScheme_presheaf_obj (V) : Œì(U, V) = Œì(X, U.Œπ ''·µÅ V) := rfl

@[simp]
lemma toScheme_presheaf_map {V W} (i : V ‚ü∂ W) :
    U.toScheme.presheaf.map i = X.presheaf.map (U.Œπ.opensFunctor.map i.unop).op := rfl

@[simp]
lemma Œπ_app (V) : U.Œπ.app V = X.presheaf.map
    (homOfLE (x := U.Œπ ''·µÅ U.Œπ ‚Åª¬π·µÅ V) (Set.image_preimage_subset _ _)).op :=
  rfl

@[simp]
lemma Œπ_appLE (V W e) :
    U.Œπ.appLE V W e =
      X.presheaf.map (homOfLE (x := U.Œπ ''·µÅ W) (Set.image_subset_iff.mpr ‚Äπ_‚Ä∫)).op := by
  simp only [Hom.appLE, Œπ_app, Functor.op_obj, Opens.carrier_eq_coe, toScheme_presheaf_map,
    Quiver.Hom.unop_op, Hom.opensFunctor_map_homOfLE, Opens.coe_inclusion, ‚Üê Functor.map_comp]
  rfl

@[simp]
lemma Œπ_appIso (V) : U.Œπ.appIso V = Iso.refl _ :=
  X.ofRestrict_appIso _ _

@[simp]
lemma opensRange_Œπ : U.Œπ.opensRange = U :=
  Opens.ext Subtype.range_val

@[simp]
lemma range_Œπ : Set.range U.Œπ.val.base = U :=
  Subtype.range_val

lemma Œπ_image_top : U.Œπ ''·µÅ ‚ä§ = U :=
  U.openEmbedding_obj_top

@[simp]
lemma Œπ_preimage_self : U.Œπ ‚Åª¬π·µÅ U = ‚ä§ :=
  Opens.inclusion_map_eq_top _

instance Œπ_appLE_isIso :
    IsIso (U.Œπ.appLE U ‚ä§ U.Œπ_preimage_self.ge) := by
  simp only [Œπ, ofRestrict_appLE]
  show IsIso (X.presheaf.map (eqToIso U.Œπ_image_top).hom.op)
  infer_instance

lemma Œπ_app_self : U.Œπ.app U = X.presheaf.map (eqToHom (X := U.Œπ ''·µÅ _) (by simp)).op := rfl

lemma eq_presheaf_map_eqToHom {V W : Opens U} (e : U.Œπ ''·µÅ V = U.Œπ ''·µÅ W) :
    X.presheaf.map (eqToHom e).op =
      U.toScheme.presheaf.map (eqToHom <| U.openEmbedding.functor_obj_injective e).op := rfl

@[simp]
lemma nonempty_iff : Nonempty U.toScheme ‚Üî (U : Set X).Nonempty := by
  simp only [toScheme_carrier, SetLike.coe_sort_coe, nonempty_subtype]
  rfl

attribute [-simp] eqToHom_op in
/-- The global sections of the restriction is isomorphic to the sections on the open set. -/
@[simps!]
def topIso : Œì(U, ‚ä§) ‚âÖ Œì(X, U) :=
  X.presheaf.mapIso (eqToIso U.Œπ_image_top.symm).op

/-- The stalks of an open subscheme are isomorphic to the stalks of the original scheme. -/
def stalkIso {X : Scheme.{u}} (U : X.Opens) (x : U) :
    U.toScheme.presheaf.stalk x ‚âÖ X.presheaf.stalk x.1 :=
  X.restrictStalkIso (Opens.openEmbedding _) _

@[reassoc (attr := simp)]
lemma germ_stalkIso_hom {X : Scheme.{u}} (U : X.Opens)
    {V : U.toScheme.Opens} (x : V) :
      U.toScheme.presheaf.germ x ‚â´ (U.stalkIso x.1).hom =
        X.presheaf.germ ‚ü®x.1.1, show x.1.1 ‚àà U.Œπ ''·µÅ V from ‚ü®x.1, x.2, rfl‚ü©‚ü© :=
    PresheafedSpace.restrictStalkIso_hom_eq_germ _ U.openEmbedding _ _ _

@[reassoc (attr := simp)]
lemma germ_stalkIso_hom' {X : Scheme.{u}} (U : X.Opens)
    {V : TopologicalSpace.Opens U} (x : U) (hx : x ‚àà V) :
      U.toScheme.presheaf.germ ‚ü®x, hx‚ü© ‚â´ (U.stalkIso x).hom =
        X.presheaf.germ ‚ü®x.1, show x.1 ‚àà U.Œπ ''·µÅ V from ‚ü®x, hx, rfl‚ü©‚ü© :=
    PresheafedSpace.restrictStalkIso_hom_eq_germ _ U.openEmbedding _ _ _

@[simp, reassoc]
lemma germ_stalkIso_inv {X : Scheme.{u}} (U : X.Opens) (V : U.toScheme.Opens) (x : U)
    (hx : x ‚àà V) : X.presheaf.germ ‚ü®x.val, show x.val ‚àà U.Œπ ''·µÅ V from ‚ü®x, hx, rfl‚ü©‚ü© ‚â´
      (U.stalkIso x).inv = U.toScheme.presheaf.germ ‚ü®x, hx‚ü© :=
  PresheafedSpace.restrictStalkIso_inv_eq_germ X.toPresheafedSpace U.openEmbedding V x hx

end Scheme.Opens

/-- If `U` is a family of open sets that covers `X`, then `X.restrict U` forms an `X.open_cover`. -/
@[simps! J obj map]
def Scheme.openCoverOfISupEqTop {s : Type*} (X : Scheme.{u}) (U : s ‚Üí X.Opens)
    (hU : ‚®Ü i, U i = ‚ä§) : X.OpenCover where
  J := s
  obj i := U i
  map i := (U i).Œπ
  f x :=
    haveI : x ‚àà ‚®Ü i, U i := hU.symm ‚ñ∏ show x ‚àà (‚ä§ : X.Opens) by trivial
    (Opens.mem_iSup.mp this).choose
  covers x := by
    erw [Subtype.range_coe]
    have : x ‚àà ‚®Ü i, U i := hU.symm ‚ñ∏ show x ‚àà (‚ä§ : X.Opens) by trivial
    exact (Opens.mem_iSup.mp this).choose_spec

@[deprecated (since := "2024-07-24")]
noncomputable alias Scheme.openCoverOfSuprEqTop := Scheme.openCoverOfISupEqTop

/-- The open sets of an open subscheme corresponds to the open sets containing in the subset. -/
@[simps!]
def opensRestrict :
    Scheme.Opens U ‚âÉ { V : X.Opens // V ‚â§ U } :=
  (IsOpenImmersion.opensEquiv (U.Œπ)).trans (Equiv.subtypeEquivProp (by simp))

instance ŒìRestrictAlgebra {X : Scheme.{u}} (U : X.Opens) :
    Algebra (Œì(X, ‚ä§)) Œì(U, ‚ä§) :=
  (U.Œπ.app ‚ä§).toAlgebra

lemma Scheme.map_basicOpen' (r : Œì(U, ‚ä§)) :
    U.Œπ ''·µÅ (U.toScheme.basicOpen r) = X.basicOpen
      (X.presheaf.map (eqToHom U.openEmbedding_obj_top.symm).op r) := by
  refine (Scheme.image_basicOpen (X.ofRestrict U.openEmbedding) r).trans ?_
  erw [‚Üê Scheme.basicOpen_res_eq _ _ (eqToHom U.openEmbedding_obj_top).op]
  rw [‚Üê comp_apply, ‚Üê CategoryTheory.Functor.map_comp, ‚Üê op_comp, eqToHom_trans, eqToHom_refl,
    op_id, CategoryTheory.Functor.map_id]
  congr
  exact PresheafedSpace.IsOpenImmersion.ofRestrict_invApp _ _ _

lemma Scheme.Opens.Œπ_image_basicOpen (r : Œì(U, ‚ä§)) :
    U.Œπ ''·µÅ (U.toScheme.basicOpen r) = X.basicOpen r := by
  rw [Scheme.map_basicOpen', Scheme.basicOpen_res_eq]

lemma Scheme.map_basicOpen_map (r : Œì(X, U)) :
    U.Œπ ''·µÅ (U.toScheme.basicOpen <| U.topIso.inv r) = X.basicOpen r := by
  simp only [Scheme.Opens.toScheme_presheaf_obj]
  rw [Scheme.map_basicOpen', Scheme.basicOpen_res_eq, Scheme.Opens.topIso_inv,
    Scheme.basicOpen_res_eq X]

-- Porting note: `simps` can't synthesize `obj_left, obj_hom, mapLeft`
variable (X) in
/-- The functor taking open subsets of `X` to open subschemes of `X`. -/
-- @[simps obj_left obj_hom mapLeft]
def Scheme.restrictFunctor : X.Opens ‚•§ Over X where
  obj U := Over.mk U.Œπ
  map {U V} i :=
    Over.homMk
      (IsOpenImmersion.lift V.Œπ U.Œπ <| by simpa using i.le)
      (IsOpenImmersion.lift_fac _ _ _)
  map_id U := by
    ext1
    dsimp only [Over.homMk_left, Over.id_left]
    rw [‚Üê cancel_mono U.Œπ, Category.id_comp,
      IsOpenImmersion.lift_fac]
  map_comp {U V W} i j := by
    ext1
    dsimp only [Over.homMk_left, Over.comp_left]
    rw [‚Üê cancel_mono W.Œπ, Category.assoc]
    iterate 3 rw [IsOpenImmersion.lift_fac]

@[simp] lemma Scheme.restrictFunctor_obj_left (U : X.Opens) :
  (X.restrictFunctor.obj U).left = U := rfl

@[simp] lemma Scheme.restrictFunctor_obj_hom (U : X.Opens) :
  (X.restrictFunctor.obj U).hom = U.Œπ := rfl

@[simp] lemma Scheme.restrictFunctor_map_left {U V : X.Opens} (i : U ‚ü∂ V) :
  (X.restrictFunctor.map i).left = IsOpenImmersion.lift (V.Œπ) U.Œπ (by simpa using i.le) := rfl

-- Porting note: the `by ...` used to be automatically done by unification magic
@[reassoc]
theorem Scheme.restrictFunctor_map_ofRestrict {U V : X.Opens} (i : U ‚ü∂ V) :
    (X.restrictFunctor.map i).1 ‚â´ V.Œπ = U.Œπ :=
  IsOpenImmersion.lift_fac _ _ (by simpa using i.le)

theorem Scheme.restrictFunctor_map_base {U V : X.Opens} (i : U ‚ü∂ V) :
    (X.restrictFunctor.map i).1.val.base = (Opens.toTopCat _).map i := by
  ext a; refine Subtype.ext ?_ -- Porting note: `ext` did not pick up `Subtype.ext`
  exact (congr_arg (fun f : X.restrict U.openEmbedding ‚ü∂ X => f.val.base a)
        (X.restrictFunctor_map_ofRestrict i))

theorem Scheme.restrictFunctor_map_app_aux {U V : X.Opens} (i : U ‚ü∂ V) (W : Opens V) :
    U.Œπ ''·µÅ ((X.restrictFunctor.map i).1 ‚Åª¬π·µÅ W) ‚â§ V.Œπ ''·µÅ W := by
  simp only [‚Üê SetLike.coe_subset_coe, IsOpenMap.functor_obj_coe, Set.image_subset_iff,
    Scheme.restrictFunctor_map_base, Opens.map_coe, Opens.inclusion_apply]
  rintro _ h
  exact ‚ü®_, h, rfl‚ü©

theorem Scheme.restrictFunctor_map_app {U V : X.Opens} (i : U ‚ü∂ V) (W : Opens V) :
    (X.restrictFunctor.map i).1.app W =
      X.presheaf.map (homOfLE <| X.restrictFunctor_map_app_aux i W).op := by
  have e‚ÇÅ := Scheme.congr_app (X.restrictFunctor_map_ofRestrict i) (V.Œπ ''·µÅ W)
  have : V.Œπ ‚Åª¬π·µÅ V.Œπ ''·µÅ W = W := W.map_functor_eq (U := V)
  have e‚ÇÇ := (X.restrictFunctor.map i).1.naturality (eqToIso this).hom.op
  have e‚ÇÉ := e‚ÇÇ.symm.trans e‚ÇÅ
  dsimp at e‚ÇÉ ‚ä¢
  rw [‚Üê IsIso.eq_comp_inv, ‚Üê Functor.map_inv, ‚Üê Functor.map_comp] at e‚ÇÉ
  rw [e‚ÇÉ, ‚Üê Functor.map_comp]
  congr 1

/-- The functor that restricts to open subschemes and then takes global section is
isomorphic to the structure sheaf. -/
@[simps!]
def Scheme.restrictFunctorŒì : X.restrictFunctor.op ‚ãô (Over.forget X).op ‚ãô Scheme.Œì ‚âÖ X.presheaf :=
  NatIso.ofComponents
    (fun U => X.presheaf.mapIso ((eqToIso (unop U).openEmbedding_obj_top).symm.op : _))
    (by
      intro U V i
      dsimp [-Scheme.restrictFunctor_map_left]
      rw [X.restrictFunctor_map_app, ‚Üê Functor.map_comp, ‚Üê Functor.map_comp]
      congr 1)

/-- `X ‚à£_ U ‚à£_ V` is isomorphic to `X ‚à£_ V ‚à£_ U` -/
noncomputable
def Scheme.restrictRestrictComm (X : Scheme.{u}) (U V : X.Opens) :
    (U.Œπ ‚Åª¬π·µÅ V).toScheme ‚âÖ V.Œπ ‚Åª¬π·µÅ U :=
  IsOpenImmersion.isoOfRangeEq (Opens.Œπ _ ‚â´ U.Œπ) (Opens.Œπ _ ‚â´ V.Œπ) <| by
    simp [Set.image_preimage_eq_inter_range, Set.inter_comm (U : Set X), Set.range_comp]

/-- If `V` is an open subset of `U`, then `X ‚à£_ U ‚à£_ V` is isomorphic to `X ‚à£_ V`. -/
noncomputable
def Scheme.restrictRestrict (X : Scheme.{u}) (U : X.Opens) (V : U.toScheme.Opens) :
    V.toScheme ‚âÖ U.Œπ ''·µÅ V :=
  IsOpenImmersion.isoOfRangeEq (Opens.Œπ _ ‚â´ U.Œπ) (Opens.Œπ _) (by simp [Set.range_comp])

@[simp, reassoc]
lemma Scheme.restrictRestrict_hom_restrict (X : Scheme.{u}) (U : X.Opens)
    (V : U.toScheme.Opens) :
    (X.restrictRestrict U V).hom ‚â´ Opens.Œπ _ = V.Œπ ‚â´ U.Œπ :=
  IsOpenImmersion.isoOfRangeEq_hom_fac _ _ _

@[simp, reassoc]
lemma Scheme.restrictRestrict_inv_restrict_restrict (X : Scheme.{u}) (U : X.Opens)
    (V : U.toScheme.Opens) :
    (X.restrictRestrict U V).inv ‚â´ V.Œπ ‚â´ U.Œπ = Opens.Œπ _ :=
  IsOpenImmersion.isoOfRangeEq_inv_fac _ _ _

/-- If `U = V`, then `X ‚à£_ U` is isomorphic to `X ‚à£_ V`. -/
noncomputable
def Scheme.restrictIsoOfEq (X : Scheme.{u}) {U V : X.Opens} (e : U = V) :
    (U : Scheme.{u}) ‚âÖ V :=
  IsOpenImmersion.isoOfRangeEq U.Œπ (V.Œπ) (by rw [e])

end

/-- The restriction of an isomorphism onto an open set. -/
noncomputable abbrev Scheme.restrictMapIso {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [IsIso f]
    (U : Y.Opens) : (f ‚Åª¬π·µÅ U).toScheme ‚âÖ U := by
  apply IsOpenImmersion.isoOfRangeEq (f := (f ‚Åª¬π·µÅ U).Œπ ‚â´ f) U.Œπ _
  dsimp
  rw [Set.range_comp, Opens.range_Œπ, Opens.range_Œπ]
  refine @Set.image_preimage_eq _ _ f.val.base U.1 f.homeomorph.surjective

section MorphismRestrict

/-- Given a morphism `f : X ‚ü∂ Y` and an open set `U ‚äÜ Y`, we have `X √ó[Y] U ‚âÖ X |_{f ‚Åª¬π U}` -/
def pullbackRestrictIsoRestrict {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : Y.Opens) :
    pullback f (U.Œπ) ‚âÖ f ‚Åª¬π·µÅ U := by
  refine IsOpenImmersion.isoOfRangeEq (pullback.fst f _) (Scheme.Opens.Œπ _) ?_
  simp [IsOpenImmersion.range_pullback_fst_of_right]

@[simp, reassoc]
theorem pullbackRestrictIsoRestrict_inv_fst {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : Y.Opens) :
    (pullbackRestrictIsoRestrict f U).inv ‚â´ pullback.fst f _ = (f ‚Åª¬π·µÅ U).Œπ := by
  delta pullbackRestrictIsoRestrict; simp

@[simp, reassoc]
theorem pullbackRestrictIsoRestrict_hom_restrict {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : Y.Opens) :
    (pullbackRestrictIsoRestrict f U).hom ‚â´ (f ‚Åª¬π·µÅ U).Œπ = pullback.fst f _ := by
  delta pullbackRestrictIsoRestrict; simp

/-- The restriction of a morphism `X ‚ü∂ Y` onto `X |_{f ‚Åª¬π U} ‚ü∂ Y |_ U`. -/
def morphismRestrict {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : Y.Opens) : (f ‚Åª¬π·µÅ U).toScheme ‚ü∂ U :=
  (pullbackRestrictIsoRestrict f U).inv ‚â´ pullback.snd _ _

/-- the notation for restricting a morphism of scheme to an open subset of the target scheme -/
infixl:85 " ‚à£_ " => morphismRestrict

@[simp, reassoc]
theorem pullbackRestrictIsoRestrict_hom_morphismRestrict {X Y : Scheme.{u}} (f : X ‚ü∂ Y)
    (U : Y.Opens) : (pullbackRestrictIsoRestrict f U).hom ‚â´ f ‚à£_ U = pullback.snd _ _ :=
  Iso.hom_inv_id_assoc _ _

@[simp, reassoc]
theorem morphismRestrict_Œπ {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : Y.Opens) :
    (f ‚à£_ U) ‚â´ U.Œπ = (f ‚Åª¬π·µÅ U).Œπ ‚â´ f := by
  delta morphismRestrict
  rw [Category.assoc, pullback.condition.symm, pullbackRestrictIsoRestrict_inv_fst_assoc]

theorem isPullback_morphismRestrict {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : Y.Opens) :
    IsPullback (f ‚à£_ U) (f ‚Åª¬π·µÅ U).Œπ U.Œπ f := by
  delta morphismRestrict
  rw [‚Üê Category.id_comp f]
  refine
    (IsPullback.of_horiz_isIso ‚ü®?_‚ü©).paste_horiz
      (IsPullback.of_hasPullback f (Y.ofRestrict U.openEmbedding)).flip
  -- Porting note: changed `rw` to `erw`
  erw [pullbackRestrictIsoRestrict_inv_fst]; rw [Category.comp_id]

@[simp]
lemma morphismRestrict_id {X : Scheme.{u}} (U : X.Opens) : ùüô X ‚à£_ U = ùüô _ := by
  rw [‚Üê cancel_mono U.Œπ, morphismRestrict_Œπ, Category.comp_id, Category.id_comp]
  rfl

theorem morphismRestrict_comp {X Y Z : Scheme.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (U : Opens Z) :
    (f ‚â´ g) ‚à£_ U = f ‚à£_ g ‚Åª¬π·µÅ U ‚â´ g ‚à£_ U := by
  delta morphismRestrict
  rw [‚Üê pullbackRightPullbackFstIso_inv_snd_snd]
  simp_rw [‚Üê Category.assoc]
  congr 1
  rw [‚Üê cancel_mono (pullback.fst _ _)]
  simp_rw [Category.assoc]
  rw [pullbackRestrictIsoRestrict_inv_fst, pullbackRightPullbackFstIso_inv_snd_fst, ‚Üê
    pullback.condition, pullbackRestrictIsoRestrict_inv_fst_assoc,
    pullbackRestrictIsoRestrict_inv_fst_assoc]
  rfl

instance {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [IsIso f] (U : Y.Opens) : IsIso (f ‚à£_ U) := by
  delta morphismRestrict; infer_instance

theorem morphismRestrict_base_coe {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : Y.Opens) (x) :
    @Coe.coe U Y (‚ü®fun x => x.1‚ü©) ((f ‚à£_ U).val.base x) = f.val.base x.1 :=
  congr_arg (fun f => PresheafedSpace.Hom.base (LocallyRingedSpace.Hom.val f) x)
    (morphismRestrict_Œπ f U)

theorem morphismRestrict_val_base {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : Y.Opens) :
    ‚áë(f ‚à£_ U).val.base = U.1.restrictPreimage f.val.base :=
  funext fun x => Subtype.ext (morphismRestrict_base_coe f U x)

theorem image_morphismRestrict_preimage {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : Y.Opens) (V : Opens U) :
    (f ‚Åª¬π·µÅ U).Œπ ''·µÅ ((f ‚à£_ U) ‚Åª¬π·µÅ V) = f ‚Åª¬π·µÅ (U.Œπ ''·µÅ V) := by
  ext1
  ext x
  constructor
  ¬∑ rintro ‚ü®‚ü®x, hx‚ü©, hx' : (f ‚à£_ U).val.base _ ‚àà V, rfl‚ü©
    refine ‚ü®‚ü®_, hx‚ü©, ?_, rfl‚ü©
    -- Porting note: this rewrite was not necessary
    rw [SetLike.mem_coe]
    convert hx'
    -- Porting note: `ext1` is not compiling
    refine Subtype.ext ?_
    exact (morphismRestrict_base_coe f U ‚ü®x, hx‚ü©).symm
  ¬∑ rintro ‚ü®‚ü®x, hx‚ü©, hx' : _ ‚àà V.1, rfl : x = _‚ü©
    refine ‚ü®‚ü®_, hx‚ü©, (?_ : (f ‚à£_ U).val.base ‚ü®x, hx‚ü© ‚àà V.1), rfl‚ü©
    convert hx'
    -- Porting note: `ext1` is compiling
    refine Subtype.ext ?_
    exact morphismRestrict_base_coe f U ‚ü®x, hx‚ü©

lemma eqToHom_eq_homOfLE {C} [Preorder C] {X Y : C} (e : X = Y) : eqToHom e = homOfLE e.le := rfl

open Scheme in
theorem morphismRestrict_app {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : Y.Opens) (V : U.toScheme.Opens) :
    (f ‚à£_ U).app V = f.app (U.Œπ ''·µÅ V) ‚â´
        X.presheaf.map (eqToHom (image_morphismRestrict_preimage f U V)).op := by
  have := Scheme.congr_app (morphismRestrict_Œπ f U) (U.Œπ ''·µÅ V)
  simp only [Scheme.preimage_comp, Opens.toScheme_presheaf_obj, Hom.app_eq_appLE, comp_appLE,
    Opens.Œπ_appLE, eqToHom_op, Opens.toScheme_presheaf_map, eqToHom_unop] at this
  have e : U.Œπ ‚Åª¬π·µÅ (U.Œπ ''·µÅ V) = V :=
    Opens.ext (Set.preimage_image_eq _ Subtype.coe_injective)
  have e' : (f ‚à£_ U) ‚Åª¬π·µÅ V = (f ‚à£_ U) ‚Åª¬π·µÅ U.Œπ ‚Åª¬π·µÅ U.Œπ ''·µÅ V := by rw [e]
  simp only [Opens.toScheme_presheaf_obj, Hom.app_eq_appLE, eqToHom_op, Hom.appLE_map]
  rw [‚Üê (f ‚à£_ U).appLE_map' _ e', ‚Üê (f ‚à£_ U).map_appLE' _ e]
  simp only [Opens.toScheme_presheaf_obj, eqToHom_eq_homOfLE, Opens.toScheme_presheaf_map,
    Quiver.Hom.unop_op, Hom.opensFunctor_map_homOfLE]
  rw [this, Hom.appLE_map, Hom.appLE_map, Hom.appLE_map]

@[simp]
theorem morphismRestrict_app' {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : Y.Opens) (V : Opens U) :
    (f ‚à£_ U).app V = f.appLE _ _ (image_morphismRestrict_preimage f U V).le :=
  morphismRestrict_app f U V

@[simp]
theorem morphismRestrict_appLE {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : Y.Opens) (V W e) :
    (f ‚à£_ U).appLE V W e = f.appLE (U.Œπ ''·µÅ V) ((f ‚Åª¬π·µÅ U).Œπ ''·µÅ W)
      ((Set.image_subset _ e).trans (image_morphismRestrict_preimage f U V).le) := by
  rw [Scheme.Hom.appLE, morphismRestrict_app', Scheme.Opens.toScheme_presheaf_map,
    Scheme.Hom.appLE_map]

theorem Œì_map_morphismRestrict {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : Y.Opens) :
    Scheme.Œì.map (f ‚à£_ U).op =
      Y.presheaf.map (eqToHom U.openEmbedding_obj_top.symm).op ‚â´
        f.app U ‚â´ X.presheaf.map (eqToHom (f ‚Åª¬π·µÅ U).openEmbedding_obj_top).op := by
  rw [Scheme.Œì_map_op, morphismRestrict_app f U ‚ä§, f.naturality_assoc, ‚Üê X.presheaf.map_comp]
  rfl

/-- Restricting a morphism onto the image of an open immersion is isomorphic to the base change
along the immersion. -/
def morphismRestrictOpensRange
    {X Y U : Scheme.{u}} (f : X ‚ü∂ Y) (g : U ‚ü∂ Y) [hg : IsOpenImmersion g] :
    Arrow.mk (f ‚à£_ g.opensRange) ‚âÖ Arrow.mk (pullback.snd f g) := by
  let V : Y.Opens := g.opensRange
  let e :=
    IsOpenImmersion.isoOfRangeEq g V.Œπ Subtype.range_coe.symm
  let t : pullback f g ‚ü∂ pullback f V.Œπ :=
    pullback.map _ _ _ _ (ùüô _) e.hom (ùüô _) (by rw [Category.comp_id, Category.id_comp])
      (by rw [Category.comp_id, IsOpenImmersion.isoOfRangeEq_hom_fac])
  symm
  refine Arrow.isoMk (asIso t ‚â™‚â´ pullbackRestrictIsoRestrict f V) e ?_
  rw [Iso.trans_hom, asIso_hom, ‚Üê Iso.comp_inv_eq, ‚Üê cancel_mono g, Arrow.mk_hom, Arrow.mk_hom,
    Category.assoc, Category.assoc, Category.assoc, IsOpenImmersion.isoOfRangeEq_inv_fac,
    ‚Üê pullback.condition, morphismRestrict_Œπ,
    pullbackRestrictIsoRestrict_hom_restrict_assoc, pullback.lift_fst_assoc, Category.comp_id]

/-- The restrictions onto two equal open sets are isomorphic. This currently has bad defeqs when
unfolded, but it should not matter for now. Replace this definition if better defeqs are needed. -/
def morphismRestrictEq {X Y : Scheme.{u}} (f : X ‚ü∂ Y) {U V : Y.Opens} (e : U = V) :
    Arrow.mk (f ‚à£_ U) ‚âÖ Arrow.mk (f ‚à£_ V) :=
  eqToIso (by subst e; rfl)

/-- Restricting a morphism twice is isomorphic to one restriction. -/
def morphismRestrictRestrict {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : Y.Opens) (V : U.toScheme.Opens) :
    Arrow.mk (f ‚à£_ U ‚à£_ V) ‚âÖ Arrow.mk (f ‚à£_ U.Œπ ''·µÅ V) := by
  refine Arrow.isoMk' _ _ (Scheme.restrictRestrict _ _ _ ‚â™‚â´ Scheme.restrictIsoOfEq _ ?_)
    (Scheme.restrictRestrict _ _ _) ?_
  ¬∑ ext x
    simp only [IsOpenMap.functor_obj_coe, Opens.coe_inclusion,
      Opens.map_coe, Set.mem_image, Set.mem_preimage, SetLike.mem_coe, morphismRestrict_val_base]
    constructor
    ¬∑ rintro ‚ü®‚ü®a, h‚ÇÅ‚ü©, h‚ÇÇ, rfl‚ü©
      exact ‚ü®_, h‚ÇÇ, rfl‚ü©
    ¬∑ rintro ‚ü®‚ü®a, h‚ÇÅ‚ü©, h‚ÇÇ, rfl : a = _‚ü©
      exact ‚ü®‚ü®x, h‚ÇÅ‚ü©, h‚ÇÇ, rfl‚ü©
  ¬∑ rw [‚Üê cancel_mono (Scheme.Opens.Œπ _), Iso.trans_hom, Category.assoc, Category.assoc,
      Category.assoc, morphismRestrict_Œπ, Scheme.restrictIsoOfEq,
      IsOpenImmersion.isoOfRangeEq_hom_fac_assoc,
      Scheme.restrictRestrict_hom_restrict_assoc,
      Scheme.restrictRestrict_hom_restrict,
      morphismRestrict_Œπ_assoc, morphismRestrict_Œπ]

/-- Restricting a morphism twice onto a basic open set is isomorphic to one restriction. -/
def morphismRestrictRestrictBasicOpen {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : Y.Opens) (r : Œì(Y, U)) :
    Arrow.mk (f ‚à£_ U ‚à£_
          U.toScheme.basicOpen (Y.presheaf.map (eqToHom U.openEmbedding_obj_top).op r)) ‚âÖ
      Arrow.mk (f ‚à£_ Y.basicOpen r) := by
  refine morphismRestrictRestrict _ _ _ ‚â™‚â´ morphismRestrictEq _ ?_
  have e := Scheme.preimage_basicOpen U.Œπ r
  rw [Scheme.Opens.Œπ_app] at e
  rw [‚Üê U.toScheme.basicOpen_res_eq _ (eqToHom U.inclusion_map_eq_top).op]
  erw [‚Üê comp_apply]
  erw [‚Üê Y.presheaf.map_comp]
  rw [eqToHom_op, eqToHom_op, eqToHom_map, eqToHom_trans]
  erw [‚Üê e]
  ext1
  dsimp [Opens.map_coe]
  rw [Set.image_preimage_eq_inter_range, Set.inter_eq_left, Scheme.Opens.range_Œπ]
  exact Y.basicOpen_le r

/-- The stalk map of a restriction of a morphism is isomorphic to the stalk map of the original map.
-/
def morphismRestrictStalkMap {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : Y.Opens) (x) :
    Arrow.mk ((f ‚à£_ U).stalkMap x) ‚âÖ Arrow.mk (f.stalkMap x.1) := Arrow.isoMk' _ _
  (U.stalkIso ((f ‚à£_ U).1.base x) ‚â™‚â´
    (TopCat.Presheaf.stalkCongr _ <| Inseparable.of_eq <| morphismRestrict_base_coe f U x))
  ((f ‚Åª¬π·µÅ U).stalkIso x) <| by
    apply TopCat.Presheaf.stalk_hom_ext
    intro V hxV
    change ‚Üë(f ‚Åª¬π·µÅ U) at x
    simp [Scheme.stalkMap_germ'_assoc, Scheme.Hom.appLE]

instance {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : Y.Opens) [IsOpenImmersion f] :
    IsOpenImmersion (f ‚à£_ U) := by
  delta morphismRestrict
  exact PresheafedSpace.IsOpenImmersion.comp _ _

end MorphismRestrict

/-- The restriction of an open cover to an open subset. -/
@[simps! J obj map]
noncomputable
def Scheme.OpenCover.restrict {X : Scheme.{u}} (ùí∞ : X.OpenCover) (U : Opens X) :
    U.toScheme.OpenCover := by
  refine copy (ùí∞.pullbackCover U.Œπ) ùí∞.J _ (ùí∞.map ¬∑ ‚à£_ U) (Equiv.refl _)
    (fun i ‚Ü¶ IsOpenImmersion.isoOfRangeEq (Opens.Œπ _) (pullback.snd _ _) ?_) ?_
  ¬∑ erw [IsOpenImmersion.range_pullback_snd_of_left U.Œπ (ùí∞.map i)]
    rw [Opens.opensRange_Œπ]
    exact Subtype.range_val
  ¬∑ intro i
    rw [‚Üê cancel_mono U.Œπ]
    simp only [morphismRestrict_Œπ, pullbackCover_J, Equiv.refl_apply, pullbackCover_obj,
      pullbackCover_map, Category.assoc, pullback.condition]
    erw [IsOpenImmersion.isoOfRangeEq_hom_fac_assoc]

end AlgebraicGeometry
