/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Geometry.RingedSpace.PresheafedSpace.Gluing
import Mathlib.AlgebraicGeometry.OpenImmersion

#align_import algebraic_geometry.gluing from "leanprover-community/mathlib"@"533f62f4dd62a5aad24a04326e6e787c8f7e98b1"

/-!
# Gluing Schemes

Given a family of gluing data of schemes, we may glue them together.

## Main definitions

* `AlgebraicGeometry.Scheme.GlueData`: A structure containing the family of gluing data.
* `AlgebraicGeometry.Scheme.GlueData.glued`: The glued scheme.
    This is defined as the multicoequalizer of `âˆ V i j â‡‰ âˆ U i`, so that the general colimit API
    can be used.
* `AlgebraicGeometry.Scheme.GlueData.Î¹`: The immersion `Î¹ i : U i âŸ¶ glued` for each `i : J`.
* `AlgebraicGeometry.Scheme.GlueData.isoCarrier`: The isomorphism between the underlying space
  of the glued scheme and the gluing of the underlying topological spaces.
* `AlgebraicGeometry.Scheme.OpenCover.gluedCover`: The glue data associated with an open cover.
* `AlgebraicGeometry.Scheme.OpenCover.fromGlued`: The canonical morphism
  `ğ’°.gluedCover.glued âŸ¶ X`. This has an `is_iso` instance.
* `AlgebraicGeometry.Scheme.OpenCover.glueMorphisms`: We may glue a family of compatible
  morphisms defined on an open cover of a scheme.

## Main results

* `AlgebraicGeometry.Scheme.GlueData.Î¹_isOpenImmersion`: The map `Î¹ i : U i âŸ¶ glued`
  is an open immersion for each `i : J`.
* `AlgebraicGeometry.Scheme.GlueData.Î¹_jointly_surjective` : The underlying maps of
  `Î¹ i : U i âŸ¶ glued` are jointly surjective.
* `AlgebraicGeometry.Scheme.GlueData.vPullbackConeIsLimit` : `V i j` is the pullback
  (intersection) of `U i` and `U j` over the glued space.
* `AlgebraicGeometry.Scheme.GlueData.Î¹_eq_iff` : `Î¹ i x = Î¹ j y` if and only if they coincide
  when restricted to `V i i`.
* `AlgebraicGeometry.Scheme.GlueData.isOpen_iff` : A subset of the glued scheme is open iff
  all its preimages in `U i` are open.

## Implementation details

All the hard work is done in `AlgebraicGeometry/PresheafedSpace/Gluing.lean` where we glue
presheafed spaces, sheafed spaces, and locally ringed spaces.

-/

set_option linter.uppercaseLean3 false

noncomputable section

universe u

open TopologicalSpace CategoryTheory Opposite

open CategoryTheory.Limits AlgebraicGeometry.PresheafedSpace

open CategoryTheory.GlueData

namespace AlgebraicGeometry

namespace Scheme

/-- A family of gluing data consists of
1. An index type `J`
2. A scheme `U i` for each `i : J`.
3. A scheme `V i j` for each `i j : J`.
  (Note that this is `J Ã— J â†’ Scheme` rather than `J â†’ J â†’ Scheme` to connect to the
  limits library easier.)
4. An open immersion `f i j : V i j âŸ¶ U i` for each `i j : Î¹`.
5. A transition map `t i j : V i j âŸ¶ V j i` for each `i j : Î¹`.
such that
6. `f i i` is an isomorphism.
7. `t i i` is the identity.
8. `V i j Ã—[U i] V i k âŸ¶ V i j âŸ¶ V j i` factors through `V j k Ã—[U j] V j i âŸ¶ V j i` via some
    `t' : V i j Ã—[U i] V i k âŸ¶ V j k Ã—[U j] V j i`.
9. `t' i j k â‰« t' j k i â‰« t' k i j = ğŸ™ _`.

We can then glue the schemes `U i` together by identifying `V i j` with `V j i`, such
that the `U i`'s are open subschemes of the glued space.
-/
-- Porting note(#5171): @[nolint has_nonempty_instance]; linter not ported yet
structure GlueData extends CategoryTheory.GlueData Scheme where
  f_open : âˆ€ i j, IsOpenImmersion (f i j)
#align algebraic_geometry.Scheme.glue_data AlgebraicGeometry.Scheme.GlueData

attribute [instance] GlueData.f_open

namespace GlueData

variable (D : GlueData.{u})

local notation "ğ–£" => D.toGlueData

/-- The glue data of locally ringed spaces associated to a family of glue data of schemes. -/
abbrev toLocallyRingedSpaceGlueData : LocallyRingedSpace.GlueData :=
  { f_open := D.f_open
    toGlueData := ğ–£.mapGlueData forgetToLocallyRingedSpace }
#align algebraic_geometry.Scheme.glue_data.to_LocallyRingedSpace_glue_data AlgebraicGeometry.Scheme.GlueData.toLocallyRingedSpaceGlueData

instance (i j : ğ–£.J) :
    LocallyRingedSpace.IsOpenImmersion ((D.toLocallyRingedSpaceGlueData).toGlueData.f i j) := by
  apply GlueData.f_open

instance (i j : ğ–£.J) :
    SheafedSpace.IsOpenImmersion
      (D.toLocallyRingedSpaceGlueData.toSheafedSpaceGlueData.toGlueData.f i j) := by
  apply GlueData.f_open

instance (i j : ğ–£.J) :
    PresheafedSpace.IsOpenImmersion
      (D.toLocallyRingedSpaceGlueData.toSheafedSpaceGlueData.toPresheafedSpaceGlueData.toGlueData.f
        i j) := by
  apply GlueData.f_open

-- Porting note: this was not needed.
instance (i : ğ–£.J) :
    LocallyRingedSpace.IsOpenImmersion ((D.toLocallyRingedSpaceGlueData).toGlueData.Î¹ i) := by
  apply LocallyRingedSpace.GlueData.Î¹_isOpenImmersion

/-- (Implementation). The glued scheme of a glue data.
This should not be used outside this file. Use `AlgebraicGeometry.Scheme.GlueData.glued` instead. -/
def gluedScheme : Scheme := by
  apply LocallyRingedSpace.IsOpenImmersion.scheme
    D.toLocallyRingedSpaceGlueData.toGlueData.glued
  intro x
  obtain âŸ¨i, y, rflâŸ© := D.toLocallyRingedSpaceGlueData.Î¹_jointly_surjective x
  refine âŸ¨?_, ?_ â‰« D.toLocallyRingedSpaceGlueData.toGlueData.Î¹ i, ?_âŸ©
  swap
  Â· exact (D.U i).affineCover.map y
  constructor
  Â· erw [TopCat.coe_comp, Set.range_comp] -- now `erw` after #13170
    refine Set.mem_image_of_mem _ ?_
    exact (D.U i).affineCover.Covers y
  Â· infer_instance
#align algebraic_geometry.Scheme.glue_data.glued_Scheme AlgebraicGeometry.Scheme.GlueData.gluedScheme

instance : CreatesColimit ğ–£.diagram.multispan forgetToLocallyRingedSpace :=
  createsColimitOfFullyFaithfulOfIso D.gluedScheme
    (HasColimit.isoOfNatIso (ğ–£.diagramIso forgetToLocallyRingedSpace).symm)

instance : PreservesColimit (ğ–£.diagram.multispan) forgetToTop :=
  inferInstanceAs (PreservesColimit (ğ–£.diagram).multispan (forgetToLocallyRingedSpace â‹™
      LocallyRingedSpace.forgetToSheafedSpace â‹™ SheafedSpace.forget CommRingCat))

instance : HasMulticoequalizer ğ–£.diagram :=
  hasColimit_of_created _ forgetToLocallyRingedSpace

/-- The glued scheme of a glued space. -/
abbrev glued : Scheme :=
  ğ–£.glued
#align algebraic_geometry.Scheme.glue_data.glued AlgebraicGeometry.Scheme.GlueData.glued

/-- The immersion from `D.U i` into the glued space. -/
abbrev Î¹ (i : D.J) : D.U i âŸ¶ D.glued :=
  ğ–£.Î¹ i
#align algebraic_geometry.Scheme.glue_data.Î¹ AlgebraicGeometry.Scheme.GlueData.Î¹

/-- The gluing as sheafed spaces is isomorphic to the gluing as presheafed spaces. -/
abbrev isoLocallyRingedSpace :
    D.glued.toLocallyRingedSpace â‰… D.toLocallyRingedSpaceGlueData.toGlueData.glued :=
  ğ–£.gluedIso forgetToLocallyRingedSpace
#align algebraic_geometry.Scheme.glue_data.iso_LocallyRingedSpace AlgebraicGeometry.Scheme.GlueData.isoLocallyRingedSpace

theorem Î¹_isoLocallyRingedSpace_inv (i : D.J) :
    D.toLocallyRingedSpaceGlueData.toGlueData.Î¹ i â‰« D.isoLocallyRingedSpace.inv = ğ–£.Î¹ i :=
  ğ–£.Î¹_gluedIso_inv forgetToLocallyRingedSpace i
#align algebraic_geometry.Scheme.glue_data.Î¹_iso_LocallyRingedSpace_inv AlgebraicGeometry.Scheme.GlueData.Î¹_isoLocallyRingedSpace_inv

instance Î¹_isOpenImmersion (i : D.J) : IsOpenImmersion (ğ–£.Î¹ i) := by
  rw [â† D.Î¹_isoLocallyRingedSpace_inv]; infer_instance
#align algebraic_geometry.Scheme.glue_data.Î¹_is_open_immersion AlgebraicGeometry.Scheme.GlueData.Î¹_isOpenImmersion

theorem Î¹_jointly_surjective (x : ğ–£.glued.carrier) :
    âˆƒ (i : D.J) (y : (D.U i).carrier), (D.Î¹ i).1.base y = x :=
  ğ–£.Î¹_jointly_surjective (forgetToTop â‹™ forget TopCat) x
#align algebraic_geometry.Scheme.glue_data.Î¹_jointly_surjective AlgebraicGeometry.Scheme.GlueData.Î¹_jointly_surjective

-- Porting note: promote to higher priority to short circuit simplifier
@[simp (high), reassoc]
theorem glue_condition (i j : D.J) : D.t i j â‰« D.f j i â‰« D.Î¹ j = D.f i j â‰« D.Î¹ i :=
  ğ–£.glue_condition i j
#align algebraic_geometry.Scheme.glue_data.glue_condition AlgebraicGeometry.Scheme.GlueData.glue_condition

/-- The pullback cone spanned by `V i j âŸ¶ U i` and `V i j âŸ¶ U j`.
This is a pullback diagram (`vPullbackConeIsLimit`). -/
def vPullbackCone (i j : D.J) : PullbackCone (D.Î¹ i) (D.Î¹ j) :=
  PullbackCone.mk (D.f i j) (D.t i j â‰« D.f j i) (by simp)
#align algebraic_geometry.Scheme.glue_data.V_pullback_cone AlgebraicGeometry.Scheme.GlueData.vPullbackCone

/-- The following diagram is a pullback, i.e. `Váµ¢â±¼` is the intersection of `Uáµ¢` and `Uâ±¼` in `X`.

Váµ¢â±¼ âŸ¶ Uáµ¢
 |      |
 â†“      â†“
 Uâ±¼ âŸ¶ X
-/
def vPullbackConeIsLimit (i j : D.J) : IsLimit (D.vPullbackCone i j) :=
  ğ–£.vPullbackConeIsLimitOfMap forgetToLocallyRingedSpace i j
    (D.toLocallyRingedSpaceGlueData.vPullbackConeIsLimit _ _)
#align algebraic_geometry.Scheme.glue_data.V_pullback_cone_is_limit AlgebraicGeometry.Scheme.GlueData.vPullbackConeIsLimit

-- Porting note: new notation
local notation "D_" => TopCat.GlueData.toGlueData <|
  D.toLocallyRingedSpaceGlueData.toSheafedSpaceGlueData.toPresheafedSpaceGlueData.toTopGlueData

/-- The underlying topological space of the glued scheme is isomorphic to the gluing of the
underlying spaces -/
def isoCarrier :
    D.glued.carrier â‰… (D_).glued := by
  refine (PresheafedSpace.forget _).mapIso ?_ â‰ªâ‰«
    GlueData.gluedIso _ (PresheafedSpace.forget.{_, _, u} _)
  refine SheafedSpace.forgetToPresheafedSpace.mapIso ?_ â‰ªâ‰«
    SheafedSpace.GlueData.isoPresheafedSpace _
  refine LocallyRingedSpace.forgetToSheafedSpace.mapIso ?_ â‰ªâ‰«
    LocallyRingedSpace.GlueData.isoSheafedSpace _
  exact Scheme.GlueData.isoLocallyRingedSpace _
#align algebraic_geometry.Scheme.glue_data.iso_carrier AlgebraicGeometry.Scheme.GlueData.isoCarrier

@[simp]
theorem Î¹_isoCarrier_inv (i : D.J) :
    (D_).Î¹ i â‰« D.isoCarrier.inv = (D.Î¹ i).1.base := by
  delta isoCarrier
  rw [Iso.trans_inv, GlueData.Î¹_gluedIso_inv_assoc, Functor.mapIso_inv, Iso.trans_inv,
    Functor.mapIso_inv, Iso.trans_inv, SheafedSpace.forgetToPresheafedSpace_map, forget_map,
    forget_map, â† comp_base, â† Category.assoc,
    D.toLocallyRingedSpaceGlueData.toSheafedSpaceGlueData.Î¹_isoPresheafedSpace_inv i]
  erw [â† Category.assoc, D.toLocallyRingedSpaceGlueData.Î¹_isoSheafedSpace_inv i]
  change (_ â‰« D.isoLocallyRingedSpace.inv).1.base = _
  rw [D.Î¹_isoLocallyRingedSpace_inv i]
#align algebraic_geometry.Scheme.glue_data.Î¹_iso_carrier_inv AlgebraicGeometry.Scheme.GlueData.Î¹_isoCarrier_inv

/-- An equivalence relation on `Î£ i, D.U i` that holds iff `ğ–£ .Î¹ i x = ğ–£ .Î¹ j y`.
See `AlgebraicGeometry.Scheme.GlueData.Î¹_eq_iff`. -/
def Rel (a b : Î£ i, ((D.U i).carrier : Type _)) : Prop :=
  a = b âˆ¨
    âˆƒ x : (D.V (a.1, b.1)).carrier, (D.f _ _).1.base x = a.2 âˆ§ (D.t _ _ â‰« D.f _ _).1.base x = b.2
#align algebraic_geometry.Scheme.glue_data.rel AlgebraicGeometry.Scheme.GlueData.Rel

theorem Î¹_eq_iff (i j : D.J) (x : (D.U i).carrier) (y : (D.U j).carrier) :
    (ğ–£.Î¹ i).1.base x = (ğ–£.Î¹ j).1.base y â†” D.Rel âŸ¨i, xâŸ© âŸ¨j, yâŸ© := by
  refine Iff.trans ?_
    (TopCat.GlueData.Î¹_eq_iff_rel
      D.toLocallyRingedSpaceGlueData.toSheafedSpaceGlueData.toPresheafedSpaceGlueData.toTopGlueData
      i j x y)
  rw [â† ((TopCat.mono_iff_injective D.isoCarrier.inv).mp _).eq_iff]
  Â· erw [â† comp_apply] -- now `erw` after #13170
    simp_rw [â† D.Î¹_isoCarrier_inv]
    rfl -- `rfl` was not needed before #13170
  Â· infer_instance
#align algebraic_geometry.Scheme.glue_data.Î¹_eq_iff AlgebraicGeometry.Scheme.GlueData.Î¹_eq_iff

theorem isOpen_iff (U : Set D.glued.carrier) : IsOpen U â†” âˆ€ i, IsOpen ((D.Î¹ i).1.base â»Â¹' U) := by
  rw [â† (TopCat.homeoOfIso D.isoCarrier.symm).isOpen_preimage]
  rw [TopCat.GlueData.isOpen_iff]
  apply forall_congr'
  intro i
  erw [â† Set.preimage_comp, â† Î¹_isoCarrier_inv]
  rfl
#align algebraic_geometry.Scheme.glue_data.is_open_iff AlgebraicGeometry.Scheme.GlueData.isOpen_iff

/-- The open cover of the glued space given by the glue data. -/
@[simps (config := .lemmasOnly)]
def openCover (D : Scheme.GlueData) : OpenCover D.glued where
  J := D.J
  obj := D.U
  map := D.Î¹
  f x := (D.Î¹_jointly_surjective x).choose
  Covers x := âŸ¨_, (D.Î¹_jointly_surjective x).choose_spec.choose_specâŸ©
#align algebraic_geometry.Scheme.glue_data.open_cover AlgebraicGeometry.Scheme.GlueData.openCover

end GlueData

namespace OpenCover

variable {X : Scheme.{u}} (ğ’° : OpenCover.{u} X)

/-- (Implementation) the transition maps in the glue data associated with an open cover. -/
def gluedCoverT' (x y z : ğ’°.J) :
    pullback (pullback.fst : pullback (ğ’°.map x) (ğ’°.map y) âŸ¶ _)
        (pullback.fst : pullback (ğ’°.map x) (ğ’°.map z) âŸ¶ _) âŸ¶
      pullback (pullback.fst : pullback (ğ’°.map y) (ğ’°.map z) âŸ¶ _)
        (pullback.fst : pullback (ğ’°.map y) (ğ’°.map x) âŸ¶ _) := by
  refine (pullbackRightPullbackFstIso _ _ _).hom â‰« ?_
  refine ?_ â‰« (pullbackSymmetry _ _).hom
  refine ?_ â‰« (pullbackRightPullbackFstIso _ _ _).inv
  refine pullback.map _ _ _ _ (pullbackSymmetry _ _).hom (ğŸ™ _) (ğŸ™ _) ?_ ?_
  Â· simp [pullback.condition]
  Â· simp
#align algebraic_geometry.Scheme.open_cover.glued_cover_t' AlgebraicGeometry.Scheme.OpenCover.gluedCoverT'

@[simp, reassoc]
theorem gluedCoverT'_fst_fst (x y z : ğ’°.J) :
    ğ’°.gluedCoverT' x y z â‰« pullback.fst â‰« pullback.fst = pullback.fst â‰« pullback.snd := by
  delta gluedCoverT'; simp
#align algebraic_geometry.Scheme.open_cover.glued_cover_t'_fst_fst AlgebraicGeometry.Scheme.OpenCover.gluedCoverT'_fst_fst

@[simp, reassoc]
theorem gluedCoverT'_fst_snd (x y z : ğ’°.J) :
    gluedCoverT' ğ’° x y z â‰« pullback.fst â‰« pullback.snd = pullback.snd â‰« pullback.snd := by
  delta gluedCoverT'; simp
#align algebraic_geometry.Scheme.open_cover.glued_cover_t'_fst_snd AlgebraicGeometry.Scheme.OpenCover.gluedCoverT'_fst_snd

@[simp, reassoc]
theorem gluedCoverT'_snd_fst (x y z : ğ’°.J) :
    gluedCoverT' ğ’° x y z â‰« pullback.snd â‰« pullback.fst = pullback.fst â‰« pullback.snd := by
  delta gluedCoverT'; simp
#align algebraic_geometry.Scheme.open_cover.glued_cover_t'_snd_fst AlgebraicGeometry.Scheme.OpenCover.gluedCoverT'_snd_fst

@[simp, reassoc]
theorem gluedCoverT'_snd_snd (x y z : ğ’°.J) :
    gluedCoverT' ğ’° x y z â‰« pullback.snd â‰« pullback.snd = pullback.fst â‰« pullback.fst := by
  delta gluedCoverT'; simp
#align algebraic_geometry.Scheme.open_cover.glued_cover_t'_snd_snd AlgebraicGeometry.Scheme.OpenCover.gluedCoverT'_snd_snd

theorem glued_cover_cocycle_fst (x y z : ğ’°.J) :
    gluedCoverT' ğ’° x y z â‰« gluedCoverT' ğ’° y z x â‰« gluedCoverT' ğ’° z x y â‰« pullback.fst =
      pullback.fst := by
  apply pullback.hom_ext <;> simp
#align algebraic_geometry.Scheme.open_cover.glued_cover_cocycle_fst AlgebraicGeometry.Scheme.OpenCover.glued_cover_cocycle_fst

theorem glued_cover_cocycle_snd (x y z : ğ’°.J) :
    gluedCoverT' ğ’° x y z â‰« gluedCoverT' ğ’° y z x â‰« gluedCoverT' ğ’° z x y â‰« pullback.snd =
      pullback.snd := by
  apply pullback.hom_ext <;> simp [pullback.condition]
#align algebraic_geometry.Scheme.open_cover.glued_cover_cocycle_snd AlgebraicGeometry.Scheme.OpenCover.glued_cover_cocycle_snd

theorem glued_cover_cocycle (x y z : ğ’°.J) :
    gluedCoverT' ğ’° x y z â‰« gluedCoverT' ğ’° y z x â‰« gluedCoverT' ğ’° z x y = ğŸ™ _ := by
  apply pullback.hom_ext <;> simp_rw [Category.id_comp, Category.assoc]
  Â· apply glued_cover_cocycle_fst
  Â· apply glued_cover_cocycle_snd
#align algebraic_geometry.Scheme.open_cover.glued_cover_cocycle AlgebraicGeometry.Scheme.OpenCover.glued_cover_cocycle

/-- The glue data associated with an open cover.
The canonical isomorphism `ğ’°.gluedCover.glued âŸ¶ X` is provided by `ğ’°.fromGlued`. -/
@[simps]
def gluedCover : Scheme.GlueData.{u} where
  J := ğ’°.J
  U := ğ’°.obj
  V := fun âŸ¨x, yâŸ© => pullback (ğ’°.map x) (ğ’°.map y)
  f x y := pullback.fst
  f_id x := inferInstance
  t x y := (pullbackSymmetry _ _).hom
  t_id x := by simp
  t' x y z := gluedCoverT' ğ’° x y z
  t_fac x y z := by apply pullback.hom_ext <;> simp
  -- The `cocycle` field could have been `by tidy` but lean timeouts.
  cocycle x y z := glued_cover_cocycle ğ’° x y z
  f_open x := inferInstance
#align algebraic_geometry.Scheme.open_cover.glued_cover AlgebraicGeometry.Scheme.OpenCover.gluedCover

/-- The canonical morphism from the gluing of an open cover of `X` into `X`.
This is an isomorphism, as witnessed by an `IsIso` instance. -/
def fromGlued : ğ’°.gluedCover.glued âŸ¶ X := by
  fapply Multicoequalizer.desc
  Â· exact fun x => ğ’°.map x
  rintro âŸ¨x, yâŸ©
  change pullback.fst â‰« _ = ((pullbackSymmetry _ _).hom â‰« pullback.fst) â‰« _
  simpa using pullback.condition
#align algebraic_geometry.Scheme.open_cover.from_glued AlgebraicGeometry.Scheme.OpenCover.fromGlued

@[simp, reassoc]
theorem Î¹_fromGlued (x : ğ’°.J) : ğ’°.gluedCover.Î¹ x â‰« ğ’°.fromGlued = ğ’°.map x :=
  Multicoequalizer.Ï€_desc _ _ _ _ _
#align algebraic_geometry.Scheme.open_cover.Î¹_from_glued AlgebraicGeometry.Scheme.OpenCover.Î¹_fromGlued

theorem fromGlued_injective : Function.Injective ğ’°.fromGlued.1.base := by
  intro x y h
  obtain âŸ¨i, x, rflâŸ© := ğ’°.gluedCover.Î¹_jointly_surjective x
  obtain âŸ¨j, y, rflâŸ© := ğ’°.gluedCover.Î¹_jointly_surjective y
  erw [â† comp_apply, â† comp_apply] at h -- now `erw` after #13170
  simp_rw [â† SheafedSpace.comp_base, â† LocallyRingedSpace.comp_val] at h
  erw [Î¹_fromGlued, Î¹_fromGlued] at h
  let e :=
    (TopCat.pullbackConeIsLimit _ _).conePointUniqueUpToIso
      (isLimitOfHasPullbackOfPreservesLimit Scheme.forgetToTop (ğ’°.map i) (ğ’°.map j))
  rw [ğ’°.gluedCover.Î¹_eq_iff]
  right
  use e.hom âŸ¨âŸ¨x, yâŸ©, hâŸ©
  constructor
  Â· erw [â† comp_apply e.hom, IsLimit.conePointUniqueUpToIso_hom_comp _ _ WalkingCospan.left]; rfl
  Â· erw [â† comp_apply e.hom, pullbackSymmetry_hom_comp_fst,
      IsLimit.conePointUniqueUpToIso_hom_comp _ _ WalkingCospan.right]
    rfl
#align algebraic_geometry.Scheme.open_cover.from_glued_injective AlgebraicGeometry.Scheme.OpenCover.fromGlued_injective

instance fromGlued_stalk_iso (x : ğ’°.gluedCover.glued.carrier) :
    IsIso (PresheafedSpace.stalkMap ğ’°.fromGlued.val x) := by
  obtain âŸ¨i, x, rflâŸ© := ğ’°.gluedCover.Î¹_jointly_surjective x
  have :=
    PresheafedSpace.stalkMap.congr_hom _ _
      (congr_arg LocallyRingedSpace.Hom.val <| ğ’°.Î¹_fromGlued i) x
  erw [PresheafedSpace.stalkMap.comp] at this
  rw [â† IsIso.eq_comp_inv] at this
  rw [this]
  infer_instance
#align algebraic_geometry.Scheme.open_cover.from_glued_stalk_iso AlgebraicGeometry.Scheme.OpenCover.fromGlued_stalk_iso

theorem fromGlued_open_map : IsOpenMap ğ’°.fromGlued.1.base := by
  intro U hU
  rw [isOpen_iff_forall_mem_open]
  intro x hx
  rw [ğ’°.gluedCover.isOpen_iff] at hU
  use ğ’°.fromGlued.val.base '' U âˆ© Set.range (ğ’°.map (ğ’°.f x)).1.base
  use Set.inter_subset_left
  constructor
  Â· rw [â† Set.image_preimage_eq_inter_range]
    apply (show IsOpenImmersion (ğ’°.map (ğ’°.f x)) from inferInstance).base_open.isOpenMap
    convert hU (ğ’°.f x) using 1
    rw [â† Î¹_fromGlued]; erw [coe_comp]; rw [Set.preimage_comp]
    congr! 1
    exact Set.preimage_image_eq _ ğ’°.fromGlued_injective
  Â· exact âŸ¨hx, ğ’°.Covers xâŸ©
#align algebraic_geometry.Scheme.open_cover.from_glued_open_map AlgebraicGeometry.Scheme.OpenCover.fromGlued_open_map

theorem fromGlued_openEmbedding : OpenEmbedding ğ’°.fromGlued.1.base :=
  -- Porting note: the continuity argument used to be `by continuity`
  openEmbedding_of_continuous_injective_open
    (ContinuousMap.continuous_toFun _) ğ’°.fromGlued_injective ğ’°.fromGlued_open_map
#align algebraic_geometry.Scheme.open_cover.from_glued_open_embedding AlgebraicGeometry.Scheme.OpenCover.fromGlued_openEmbedding

instance : Epi ğ’°.fromGlued.val.base := by
  rw [TopCat.epi_iff_surjective]
  intro x
  obtain âŸ¨y, hâŸ© := ğ’°.Covers x
  use (ğ’°.gluedCover.Î¹ (ğ’°.f x)).1.base y
  erw [â† comp_apply] -- now `erw` after #13170
  rw [â† ğ’°.Î¹_fromGlued (ğ’°.f x)] at h
  exact h

instance fromGlued_open_immersion : IsOpenImmersion ğ’°.fromGlued :=
  SheafedSpace.IsOpenImmersion.of_stalk_iso _ ğ’°.fromGlued_openEmbedding
#align algebraic_geometry.Scheme.open_cover.from_glued_open_immersion AlgebraicGeometry.Scheme.OpenCover.fromGlued_open_immersion

instance : IsIso ğ’°.fromGlued :=
  let F := Scheme.forgetToLocallyRingedSpace â‹™ LocallyRingedSpace.forgetToSheafedSpace â‹™
    SheafedSpace.forgetToPresheafedSpace
  have : IsIso (F.map (fromGlued ğ’°)) := by
    change @IsIso (PresheafedSpace _) _ _ _ ğ’°.fromGlued.val
    apply PresheafedSpace.IsOpenImmersion.to_iso
  isIso_of_reflects_iso _ F

/-- Given an open cover of `X`, and a morphism `ğ’°.obj x âŸ¶ Y` for each open subscheme in the cover,
such that these morphisms are compatible in the intersection (pullback), we may glue the morphisms
together into a morphism `X âŸ¶ Y`.

Note:
If `X` is exactly (defeq to) the gluing of `U i`, then using `Multicoequalizer.desc` suffices.
-/
def glueMorphisms {Y : Scheme} (f : âˆ€ x, ğ’°.obj x âŸ¶ Y)
    (hf : âˆ€ x y, (pullback.fst : pullback (ğ’°.map x) (ğ’°.map y) âŸ¶ _) â‰« f x = pullback.snd â‰« f y) :
    X âŸ¶ Y := by
  refine inv ğ’°.fromGlued â‰« ?_
  fapply Multicoequalizer.desc
  Â· exact f
  rintro âŸ¨i, jâŸ©
  change pullback.fst â‰« f i = (_ â‰« _) â‰« f j
  erw [pullbackSymmetry_hom_comp_fst]
  exact hf i j
#align algebraic_geometry.Scheme.open_cover.glue_morphisms AlgebraicGeometry.Scheme.OpenCover.glueMorphisms

@[simp, reassoc]
theorem Î¹_glueMorphisms {Y : Scheme} (f : âˆ€ x, ğ’°.obj x âŸ¶ Y)
    (hf : âˆ€ x y, (pullback.fst : pullback (ğ’°.map x) (ğ’°.map y) âŸ¶ _) â‰« f x = pullback.snd â‰« f y)
    (x : ğ’°.J) : ğ’°.map x â‰« ğ’°.glueMorphisms f hf = f x := by
  rw [â† Î¹_fromGlued, Category.assoc]
  erw [IsIso.hom_inv_id_assoc, Multicoequalizer.Ï€_desc]
#align algebraic_geometry.Scheme.open_cover.Î¹_glue_morphisms AlgebraicGeometry.Scheme.OpenCover.Î¹_glueMorphisms

theorem hom_ext {Y : Scheme} (fâ‚ fâ‚‚ : X âŸ¶ Y) (h : âˆ€ x, ğ’°.map x â‰« fâ‚ = ğ’°.map x â‰« fâ‚‚) : fâ‚ = fâ‚‚ := by
  rw [â† cancel_epi ğ’°.fromGlued]
  apply Multicoequalizer.hom_ext
  intro x
  erw [Multicoequalizer.Ï€_desc_assoc]
  erw [Multicoequalizer.Ï€_desc_assoc]
  exact h x
#align algebraic_geometry.Scheme.open_cover.hom_ext AlgebraicGeometry.Scheme.OpenCover.hom_ext

end OpenCover

end Scheme

end AlgebraicGeometry
