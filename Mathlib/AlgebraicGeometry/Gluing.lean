/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Restrict
import Mathlib.CategoryTheory.LocallyDirected
import Mathlib.Geometry.RingedSpace.PresheafedSpace.Gluing

/-!
# Gluing Schemes

Given a family of gluing data of schemes, we may glue them together.
Also see the section about "locally directed" gluing,
which is a special case where the conditions are easier to check.

## Main definitions

* `AlgebraicGeometry.Scheme.GlueData`: A structure containing the family of gluing data.
* `AlgebraicGeometry.Scheme.GlueData.glued`: The glued scheme.
    This is defined as the multicoequalizer of `âˆ V i j â‡‰ âˆ U i`, so that the general colimit API
    can be used.
* `AlgebraicGeometry.Scheme.GlueData.Î¹`: The immersion `Î¹ i : U i âŸ¶ glued` for each `i : J`.
* `AlgebraicGeometry.Scheme.GlueData.isoCarrier`: The isomorphism between the underlying space
  of the glued scheme and the gluing of the underlying topological spaces.
* `AlgebraicGeometry.Scheme.OpenCover.gluedCover`: The glue data associated with an open cover.
* `AlgebraicGeometry.Scheme.OpenCover.fromGlued`: The canonical morphism
  `ğ’°.gluedCover.glued âŸ¶ X`. This has an `is_iso` instance.
* `AlgebraicGeometry.Scheme.OpenCover.glueMorphisms`: We may glue a family of compatible
  morphisms defined on an open cover of a scheme.

## Main results

* `AlgebraicGeometry.Scheme.GlueData.Î¹_isOpenImmersion`: The map `Î¹ i : U i âŸ¶ glued`
  is an open immersion for each `i : J`.
* `AlgebraicGeometry.Scheme.GlueData.Î¹_jointly_surjective` : The underlying maps of
  `Î¹ i : U i âŸ¶ glued` are jointly surjective.
* `AlgebraicGeometry.Scheme.GlueData.vPullbackConeIsLimit` : `V i j` is the pullback
  (intersection) of `U i` and `U j` over the glued space.
* `AlgebraicGeometry.Scheme.GlueData.Î¹_eq_iff` : `Î¹ i x = Î¹ j y` if and only if they coincide
  when restricted to `V i i`.
* `AlgebraicGeometry.Scheme.GlueData.isOpen_iff` : A subset of the glued scheme is open iff
  all its preimages in `U i` are open.

## Implementation details

All the hard work is done in `AlgebraicGeometry/PresheafedSpace/Gluing.lean` where we glue
presheafed spaces, sheafed spaces, and locally ringed spaces.

-/


noncomputable section

universe v u

open TopologicalSpace CategoryTheory Opposite Topology

open CategoryTheory.Limits AlgebraicGeometry.PresheafedSpace

open CategoryTheory.GlueData

namespace AlgebraicGeometry

namespace Scheme

/-- A family of gluing data consists of
1. An index type `J`
2. A scheme `U i` for each `i : J`.
3. A scheme `V i j` for each `i j : J`.
  (Note that this is `J Ã— J â†’ Scheme` rather than `J â†’ J â†’ Scheme` to connect to the
  limits library easier.)
4. An open immersion `f i j : V i j âŸ¶ U i` for each `i j : Î¹`.
5. A transition map `t i j : V i j âŸ¶ V j i` for each `i j : Î¹`.
such that
6. `f i i` is an isomorphism.
7. `t i i` is the identity.
8. `V i j Ã—[U i] V i k âŸ¶ V i j âŸ¶ V j i` factors through `V j k Ã—[U j] V j i âŸ¶ V j i` via some
    `t' : V i j Ã—[U i] V i k âŸ¶ V j k Ã—[U j] V j i`.
9. `t' i j k â‰« t' j k i â‰« t' k i j = ğŸ™ _`.

We can then glue the schemes `U i` together by identifying `V i j` with `V j i`, such
that the `U i`'s are open subschemes of the glued space.
-/
structure GlueData extends CategoryTheory.GlueData Scheme where
  f_open : âˆ€ i j, IsOpenImmersion (f i j)

attribute [instance] GlueData.f_open

namespace GlueData

variable (D : GlueData.{u})

local notation "ğ–£" => D.toGlueData

/-- The glue data of locally ringed spaces associated to a family of glue data of schemes. -/
abbrev toLocallyRingedSpaceGlueData : LocallyRingedSpace.GlueData :=
  { f_open := D.f_open
    toGlueData := ğ–£.mapGlueData forgetToLocallyRingedSpace }

instance (i j : ğ–£.J) :
    LocallyRingedSpace.IsOpenImmersion ((D.toLocallyRingedSpaceGlueData).toGlueData.f i j) := by
  apply GlueData.f_open

instance (i j : ğ–£.J) :
    SheafedSpace.IsOpenImmersion
      (D.toLocallyRingedSpaceGlueData.toSheafedSpaceGlueData.toGlueData.f i j) := by
  apply GlueData.f_open

instance (i j : ğ–£.J) :
    PresheafedSpace.IsOpenImmersion
      (D.toLocallyRingedSpaceGlueData.toSheafedSpaceGlueData.toPresheafedSpaceGlueData.toGlueData.f
        i j) := by
  apply GlueData.f_open

instance (i : ğ–£.J) :
    LocallyRingedSpace.IsOpenImmersion ((D.toLocallyRingedSpaceGlueData).toGlueData.Î¹ i) := by
  apply LocallyRingedSpace.GlueData.Î¹_isOpenImmersion

/-- (Implementation). The glued scheme of a glue data.
This should not be used outside this file. Use `AlgebraicGeometry.Scheme.GlueData.glued` instead. -/
def gluedScheme : Scheme := by
  apply LocallyRingedSpace.IsOpenImmersion.scheme
    D.toLocallyRingedSpaceGlueData.toGlueData.glued
  intro x
  obtain âŸ¨i, y, rflâŸ© := D.toLocallyRingedSpaceGlueData.Î¹_jointly_surjective x
  refine âŸ¨_, ((D.U i).affineCover.map y).toLRSHom â‰«
    D.toLocallyRingedSpaceGlueData.toGlueData.Î¹ i, ?_âŸ©
  constructor
  Â· simp only [LocallyRingedSpace.comp_toShHom, SheafedSpace.comp_base, TopCat.hom_comp,
      ContinuousMap.coe_comp, Set.range_comp]
    refine Set.mem_image_of_mem _ ?_
    exact (D.U i).affineCover.covers y
  Â· infer_instance

instance : CreatesColimit ğ–£.diagram.multispan forgetToLocallyRingedSpace :=
  createsColimitOfFullyFaithfulOfIso D.gluedScheme
    (HasColimit.isoOfNatIso (ğ–£.diagramIso forgetToLocallyRingedSpace).symm)

instance : PreservesColimit (ğ–£.diagram.multispan) forgetToTop :=
  inferInstanceAs (PreservesColimit (ğ–£.diagram).multispan (forgetToLocallyRingedSpace â‹™
      LocallyRingedSpace.forgetToSheafedSpace â‹™ SheafedSpace.forget CommRingCat))

instance : PreservesColimit (ğ–£.diagram.multispan) forget :=
  inferInstanceAs (PreservesColimit (ğ–£.diagram).multispan (forgetToTop â‹™ CategoryTheory.forget _))

instance : HasMulticoequalizer ğ–£.diagram :=
  hasColimit_of_created _ forgetToLocallyRingedSpace

/-- The glued scheme of a glued space. -/
abbrev glued : Scheme :=
  ğ–£.glued

/-- The immersion from `D.U i` into the glued space. -/
abbrev Î¹ (i : D.J) : D.U i âŸ¶ D.glued :=
  ğ–£.Î¹ i

/-- The gluing as sheafed spaces is isomorphic to the gluing as presheafed spaces. -/
abbrev isoLocallyRingedSpace :
    D.glued.toLocallyRingedSpace â‰… D.toLocallyRingedSpaceGlueData.toGlueData.glued :=
  ğ–£.gluedIso forgetToLocallyRingedSpace

theorem Î¹_isoLocallyRingedSpace_inv (i : D.J) :
    D.toLocallyRingedSpaceGlueData.toGlueData.Î¹ i â‰«
      D.isoLocallyRingedSpace.inv = (ğ–£.Î¹ i).toLRSHom :=
  ğ–£.Î¹_gluedIso_inv forgetToLocallyRingedSpace i

instance Î¹_isOpenImmersion (i : D.J) : IsOpenImmersion (ğ–£.Î¹ i) := by
  rw [IsOpenImmersion, â† D.Î¹_isoLocallyRingedSpace_inv]; infer_instance

theorem Î¹_jointly_surjective (x : ğ–£.glued.carrier) :
    âˆƒ (i : D.J) (y : (D.U i).carrier), (D.Î¹ i).base y = x :=
  ğ–£.Î¹_jointly_surjective forget x

/-- Promoted to higher priority to short circuit simplifier. -/
@[simp (high), reassoc]
theorem glue_condition (i j : D.J) : D.t i j â‰« D.f j i â‰« D.Î¹ j = D.f i j â‰« D.Î¹ i :=
  ğ–£.glue_condition i j

/-- The pullback cone spanned by `V i j âŸ¶ U i` and `V i j âŸ¶ U j`.
This is a pullback diagram (`vPullbackConeIsLimit`). -/
def vPullbackCone (i j : D.J) : PullbackCone (D.Î¹ i) (D.Î¹ j) :=
  PullbackCone.mk (D.f i j) (D.t i j â‰« D.f j i) (by simp)

/-- The following diagram is a pullback, i.e. `Váµ¢â±¼` is the intersection of `Uáµ¢` and `Uâ±¼` in `X`.
```
Váµ¢â±¼ âŸ¶ Uáµ¢
 |      |
 â†“      â†“
 Uâ±¼ âŸ¶ X
```
-/
def vPullbackConeIsLimit (i j : D.J) : IsLimit (D.vPullbackCone i j) :=
  ğ–£.vPullbackConeIsLimitOfMap forgetToLocallyRingedSpace i j
    (D.toLocallyRingedSpaceGlueData.vPullbackConeIsLimit _ _)

local notation "D_" => TopCat.GlueData.toGlueData <|
  D.toLocallyRingedSpaceGlueData.toSheafedSpaceGlueData.toPresheafedSpaceGlueData.toTopGlueData

/-- The underlying topological space of the glued scheme is isomorphic to the gluing of the
underlying spaces -/
def isoCarrier :
    D.glued.carrier â‰… (D_).glued := by
  refine (PresheafedSpace.forget _).mapIso ?_ â‰ªâ‰«
    GlueData.gluedIso _ (PresheafedSpace.forget.{_, _, u} _)
  refine SheafedSpace.forgetToPresheafedSpace.mapIso ?_ â‰ªâ‰«
    SheafedSpace.GlueData.isoPresheafedSpace _
  refine LocallyRingedSpace.forgetToSheafedSpace.mapIso ?_ â‰ªâ‰«
    LocallyRingedSpace.GlueData.isoSheafedSpace _
  exact Scheme.GlueData.isoLocallyRingedSpace _

@[simp]
theorem Î¹_isoCarrier_inv (i : D.J) :
    (D_).Î¹ i â‰« D.isoCarrier.inv = (D.Î¹ i).base := by
  delta isoCarrier
  rw [Iso.trans_inv, GlueData.Î¹_gluedIso_inv_assoc, Functor.mapIso_inv, Iso.trans_inv,
    Functor.mapIso_inv, Iso.trans_inv, SheafedSpace.forgetToPresheafedSpace_map,
    PresheafedSpace.forget_map,
    PresheafedSpace.forget_map, â† PresheafedSpace.comp_base, â† Category.assoc,
    D.toLocallyRingedSpaceGlueData.toSheafedSpaceGlueData.Î¹_isoPresheafedSpace_inv i]
  erw [â† Category.assoc, D.toLocallyRingedSpaceGlueData.Î¹_isoSheafedSpace_inv i]
  change (_ â‰« D.isoLocallyRingedSpace.inv).base = _
  rw [D.Î¹_isoLocallyRingedSpace_inv i]

/-- An equivalence relation on `Î£ i, D.U i` that holds iff `ğ–£.Î¹ i x = ğ–£.Î¹ j y`.
See `AlgebraicGeometry.Scheme.GlueData.Î¹_eq_iff`. -/
def Rel (a b : Î£ i, ((D.U i).carrier : Type _)) : Prop :=
  âˆƒ x : (D.V (a.1, b.1)).carrier, (D.f _ _).base x = a.2 âˆ§ (D.t _ _ â‰« D.f _ _).base x = b.2

theorem Î¹_eq_iff (i j : D.J) (x : (D.U i).carrier) (y : (D.U j).carrier) :
    (ğ–£.Î¹ i).base x = (ğ–£.Î¹ j).base y â†” D.Rel âŸ¨i, xâŸ© âŸ¨j, yâŸ© := by
  refine Iff.trans ?_
    (TopCat.GlueData.Î¹_eq_iff_rel
      D.toLocallyRingedSpaceGlueData.toSheafedSpaceGlueData.toPresheafedSpaceGlueData.toTopGlueData
      i j x y)
  rw [â† ((TopCat.mono_iff_injective D.isoCarrier.inv).mp _).eq_iff, â† ConcreteCategory.comp_apply]
  Â· simp_rw [â† D.Î¹_isoCarrier_inv]
    rfl -- `rfl` was not needed before https://github.com/leanprover-community/mathlib4/pull/13170
  Â· infer_instance

theorem isOpen_iff (U : Set D.glued.carrier) : IsOpen U â†” âˆ€ i, IsOpen ((D.Î¹ i).base â»Â¹' U) := by
  rw [â† (TopCat.homeoOfIso D.isoCarrier.symm).isOpen_preimage, TopCat.GlueData.isOpen_iff]
  apply forall_congr'
  intro i
  rw [â† Set.preimage_comp, â† Î¹_isoCarrier_inv]
  rfl

/-- The open cover of the glued space given by the glue data. -/
@[simps -isSimp]
def openCover (D : Scheme.GlueData) : OpenCover D.glued where
  J := D.J
  obj := D.U
  map := D.Î¹
  f x := (D.Î¹_jointly_surjective x).choose
  covers x := âŸ¨_, (D.Î¹_jointly_surjective x).choose_spec.choose_specâŸ©

end GlueData

namespace Cover

variable {X : Scheme.{u}} (ğ’° : OpenCover.{u} X)

/-- (Implementation) the transition maps in the glue data associated with an open cover. -/
def gluedCoverT' (x y z : ğ’°.J) :
    pullback (pullback.fst (ğ’°.map x) (ğ’°.map y)) (pullback.fst (ğ’°.map x) (ğ’°.map z)) âŸ¶
      pullback (pullback.fst (ğ’°.map y) (ğ’°.map z)) (pullback.fst (ğ’°.map y) (ğ’°.map x)) := by
  refine (pullbackRightPullbackFstIso _ _ _).hom â‰« ?_
  refine ?_ â‰« (pullbackSymmetry _ _).hom
  refine ?_ â‰« (pullbackRightPullbackFstIso _ _ _).inv
  refine pullback.map _ _ _ _ (pullbackSymmetry _ _).hom (ğŸ™ _) (ğŸ™ _) ?_ ?_
  Â· simp [pullback.condition]
  Â· simp

@[simp, reassoc]
theorem gluedCoverT'_fst_fst (x y z : ğ’°.J) :
    ğ’°.gluedCoverT' x y z â‰« pullback.fst _ _ â‰« pullback.fst _ _ =
      pullback.fst _ _ â‰« pullback.snd _ _ := by
  delta gluedCoverT'; simp

@[simp, reassoc]
theorem gluedCoverT'_fst_snd (x y z : ğ’°.J) :
    gluedCoverT' ğ’° x y z â‰« pullback.fst _ _ â‰« pullback.snd _ _ =
      pullback.snd _ _ â‰« pullback.snd _ _ := by
  delta gluedCoverT'; simp

@[simp, reassoc]
theorem gluedCoverT'_snd_fst (x y z : ğ’°.J) :
    gluedCoverT' ğ’° x y z â‰« pullback.snd _ _ â‰« pullback.fst _ _ =
      pullback.fst _ _ â‰« pullback.snd _ _ := by
  delta gluedCoverT'; simp

@[simp, reassoc]
theorem gluedCoverT'_snd_snd (x y z : ğ’°.J) :
    gluedCoverT' ğ’° x y z â‰« pullback.snd _ _ â‰« pullback.snd _ _ =
      pullback.fst _ _ â‰« pullback.fst _ _ := by
  delta gluedCoverT'; simp

theorem glued_cover_cocycle_fst (x y z : ğ’°.J) :
    gluedCoverT' ğ’° x y z â‰« gluedCoverT' ğ’° y z x â‰« gluedCoverT' ğ’° z x y â‰« pullback.fst _ _ =
      pullback.fst _ _ := by
  apply pullback.hom_ext <;> simp

theorem glued_cover_cocycle_snd (x y z : ğ’°.J) :
    gluedCoverT' ğ’° x y z â‰« gluedCoverT' ğ’° y z x â‰« gluedCoverT' ğ’° z x y â‰« pullback.snd _ _ =
      pullback.snd _ _ := by
  apply pullback.hom_ext <;> simp [pullback.condition]

theorem glued_cover_cocycle (x y z : ğ’°.J) :
    gluedCoverT' ğ’° x y z â‰« gluedCoverT' ğ’° y z x â‰« gluedCoverT' ğ’° z x y = ğŸ™ _ := by
  apply pullback.hom_ext <;> simp_rw [Category.id_comp, Category.assoc]
  Â· apply glued_cover_cocycle_fst
  Â· apply glued_cover_cocycle_snd

/-- The glue data associated with an open cover.
The canonical isomorphism `ğ’°.gluedCover.glued âŸ¶ X` is provided by `ğ’°.fromGlued`. -/
@[simps]
def gluedCover : Scheme.GlueData.{u} where
  J := ğ’°.J
  U := ğ’°.obj
  V := fun âŸ¨x, yâŸ© => pullback (ğ’°.map x) (ğ’°.map y)
  f _ _ := pullback.fst _ _
  f_id _ := inferInstance
  t _ _ := (pullbackSymmetry _ _).hom
  t_id x := by simp
  t' x y z := gluedCoverT' ğ’° x y z
  t_fac x y z := by apply pullback.hom_ext <;> simp
  -- The `cocycle` field could have been `by tidy` but lean timeouts.
  cocycle x y z := glued_cover_cocycle ğ’° x y z
  f_open _ := inferInstance

/-- The canonical morphism from the gluing of an open cover of `X` into `X`.
This is an isomorphism, as witnessed by an `IsIso` instance. -/
def fromGlued : ğ’°.gluedCover.glued âŸ¶ X := by
  fapply Multicoequalizer.desc
  Â· exact fun x => ğ’°.map x
  rintro âŸ¨x, yâŸ©
  change pullback.fst _ _ â‰« _ = ((pullbackSymmetry _ _).hom â‰« pullback.fst _ _) â‰« _
  simpa using pullback.condition

@[simp, reassoc]
theorem Î¹_fromGlued (x : ğ’°.J) : ğ’°.gluedCover.Î¹ x â‰« ğ’°.fromGlued = ğ’°.map x :=
  Multicoequalizer.Ï€_desc _ _ _ _ _

theorem fromGlued_injective : Function.Injective ğ’°.fromGlued.base := by
  intro x y h
  obtain âŸ¨i, x, rflâŸ© := ğ’°.gluedCover.Î¹_jointly_surjective x
  obtain âŸ¨j, y, rflâŸ© := ğ’°.gluedCover.Î¹_jointly_surjective y
  rw [â† ConcreteCategory.comp_apply, â† ConcreteCategory.comp_apply] at h
  simp_rw [â† Scheme.comp_base] at h
  rw [Î¹_fromGlued, Î¹_fromGlued] at h
  let e :=
    (TopCat.pullbackConeIsLimit _ _).conePointUniqueUpToIso
      (isLimitOfHasPullbackOfPreservesLimit Scheme.forgetToTop (ğ’°.map i) (ğ’°.map j))
  rw [ğ’°.gluedCover.Î¹_eq_iff]
  use e.hom âŸ¨âŸ¨x, yâŸ©, hâŸ©
  constructor
  Â· erw [â† ConcreteCategory.comp_apply e.hom,
      IsLimit.conePointUniqueUpToIso_hom_comp _ _ WalkingCospan.left]
    rfl
  Â· erw [â† ConcreteCategory.comp_apply e.hom, pullbackSymmetry_hom_comp_fst,
      IsLimit.conePointUniqueUpToIso_hom_comp _ _ WalkingCospan.right]
    rfl

instance fromGlued_stalk_iso (x : ğ’°.gluedCover.glued.carrier) :
    IsIso (ğ’°.fromGlued.stalkMap x) := by
  obtain âŸ¨i, x, rflâŸ© := ğ’°.gluedCover.Î¹_jointly_surjective x
  have := stalkMap_congr_hom _ _ (ğ’°.Î¹_fromGlued i) x
  rw [stalkMap_comp, â† IsIso.eq_comp_inv] at this
  rw [this]
  infer_instance

theorem fromGlued_open_map : IsOpenMap ğ’°.fromGlued.base := by
  intro U hU
  rw [isOpen_iff_forall_mem_open]
  intro x hx
  rw [ğ’°.gluedCover.isOpen_iff] at hU
  use ğ’°.fromGlued.base '' U âˆ© Set.range (ğ’°.map (ğ’°.f x)).base
  use Set.inter_subset_left
  constructor
  Â· rw [â† Set.image_preimage_eq_inter_range]
    apply (show IsOpenImmersion (ğ’°.map (ğ’°.f x)) from inferInstance).base_open.isOpenMap
    convert hU (ğ’°.f x) using 1
    simp only [â† Î¹_fromGlued, gluedCover_U, comp_coeBase, TopCat.hom_comp, ContinuousMap.coe_comp,
      Set.preimage_comp]
    congr! 1
    exact Set.preimage_image_eq _ ğ’°.fromGlued_injective
  Â· exact âŸ¨hx, ğ’°.covers xâŸ©

theorem fromGlued_isOpenEmbedding : IsOpenEmbedding ğ’°.fromGlued.base :=
  .of_continuous_injective_isOpenMap (by fun_prop) ğ’°.fromGlued_injective ğ’°.fromGlued_open_map

instance : Epi ğ’°.fromGlued.base := by
  rw [TopCat.epi_iff_surjective]
  intro x
  obtain âŸ¨y, hâŸ© := ğ’°.covers x
  use (ğ’°.gluedCover.Î¹ (ğ’°.f x)).base y
  rw [â† ConcreteCategory.comp_apply]
  rw [â† ğ’°.Î¹_fromGlued (ğ’°.f x)] at h
  exact h

instance fromGlued_open_immersion : IsOpenImmersion ğ’°.fromGlued :=
  IsOpenImmersion.of_stalk_iso _ ğ’°.fromGlued_isOpenEmbedding

instance : IsIso ğ’°.fromGlued :=
  let F := Scheme.forgetToLocallyRingedSpace â‹™ LocallyRingedSpace.forgetToSheafedSpace â‹™
    SheafedSpace.forgetToPresheafedSpace
  have : IsIso (F.map (fromGlued ğ’°)) := by
    change IsIso ğ’°.fromGlued.toPshHom
    apply PresheafedSpace.IsOpenImmersion.to_iso
  isIso_of_reflects_iso _ F

/-- Given an open cover of `X`, and a morphism `ğ’°.obj x âŸ¶ Y` for each open subscheme in the cover,
such that these morphisms are compatible in the intersection (pullback), we may glue the morphisms
together into a morphism `X âŸ¶ Y`.

Note:
If `X` is exactly (defeq to) the gluing of `U i`, then using `Multicoequalizer.desc` suffices.
-/
def glueMorphisms (ğ’° : OpenCover.{v} X) {Y : Scheme.{u}} (f : âˆ€ x, ğ’°.obj x âŸ¶ Y)
    (hf : âˆ€ x y, pullback.fst (ğ’°.map x) (ğ’°.map y) â‰« f x = pullback.snd _ _ â‰« f y) :
    X âŸ¶ Y := by
  refine inv ğ’°.ulift.fromGlued â‰« ?_
  fapply Multicoequalizer.desc
  Â· exact fun i â†¦ f _
  rintro âŸ¨i, jâŸ©
  dsimp
  change pullback.fst _ _ â‰« f _ = (_ â‰« _) â‰« f _
  simp [pullbackSymmetry_hom_comp_fst]
  exact hf _ _

theorem hom_ext (ğ’° : OpenCover.{v} X) {Y : Scheme} (fâ‚ fâ‚‚ : X âŸ¶ Y)
    (h : âˆ€ x, ğ’°.map x â‰« fâ‚ = ğ’°.map x â‰« fâ‚‚) : fâ‚ = fâ‚‚ := by
  rw [â† cancel_epi ğ’°.ulift.fromGlued]
  apply Multicoequalizer.hom_ext
  intro x
  rw [fromGlued, Multicoequalizer.Ï€_desc_assoc, Multicoequalizer.Ï€_desc_assoc]
  exact h _

@[simp, reassoc]
theorem Î¹_glueMorphisms (ğ’° : OpenCover.{v} X) {Y : Scheme} (f : âˆ€ x, ğ’°.obj x âŸ¶ Y)
    (hf : âˆ€ x y, pullback.fst (ğ’°.map x) (ğ’°.map y) â‰« f x = pullback.snd _ _ â‰« f y)
    (x : ğ’°.J) : ğ’°.map x â‰« ğ’°.glueMorphisms f hf = f x := by
  refine (ğ’°.ulift.pullbackCover (ğ’°.map x)).hom_ext _ _ fun i â†¦ ?_
  dsimp only [ulift_J, ulift_obj, ulift_map, pullbackCover_obj, pullbackCover_map]
  simp_rw [pullback.condition_assoc, â† ulift_map, â† Î¹_fromGlued, Category.assoc, glueMorphisms,
    IsIso.hom_inv_id_assoc, ulift_map, hf]
  erw [Multicoequalizer.Ï€_desc]

end Cover

lemma hom_ext_of_forall {X Y : Scheme} (f g : X âŸ¶ Y)
    (H : âˆ€ x : X, âˆƒ U : X.Opens, x âˆˆ U âˆ§ U.Î¹ â‰« f = U.Î¹ â‰« g) : f = g := by
  choose U hxU hU using H
  let ğ’° : X.OpenCover := { J := X, obj i := (U i), map i := (U i).Î¹, f x := x, covers := by simpa }
  exact ğ’°.hom_ext _ _ hU

/-!

## Locally directed gluing

We say that a diagram of open immersions is "locally directed" if for any `V, W âŠ† U` in the diagram,
`V âˆ© W` is a union of elements in the diagram. Equivalently, for every `x âˆˆ U` in the diagram,
the set of elements containing `x` is directed (and hence the name).

For such a diagram, we can glue them directly since the gluing conditions are always satisfied.
The intended usage is to provide the following instances:
- `âˆ€ {i j} (f : i âŸ¶ j), IsOpenImmersion (F.map f)`
- `(F â‹™ forget).IsLocallyDirected`
and to directly use the `colimit` API.
Also see `AlgebraicGeometry.Scheme.IsLocallyDirected.openCover` for the open cover of the `colimit`.

-/
section IsLocallyDirected

open TopologicalSpace.Opens

universe w

variable {J : Type w} [Category.{v} J] (F : J â¥¤ Scheme.{u})
variable [âˆ€ {i j} (f : i âŸ¶ j), IsOpenImmersion (F.map f)]

namespace IsLocallyDirected

/-- (Implemetation detail)
The intersection `V` in the glue data associated to a locally directed diagram. -/
noncomputable
def V (i j : J) : (F.obj i).Opens := â¨† (k : Î£ k, (k âŸ¶ i) Ã— (k âŸ¶ j)), (F.map k.2.1).opensRange

lemma V_self (i) : V F i i = âŠ¤ :=
  top_le_iff.mp (le_iSup_of_le âŸ¨i, ğŸ™ _, ğŸ™ _âŸ© (by simp [Scheme.Hom.opensRange_of_isIso]))

variable [(F â‹™ forget).IsLocallyDirected]

lemma exists_of_pullback_V_V {i j k : J} (x : pullback (C := Scheme) (V F i j).Î¹ (V F i k).Î¹) :
    âˆƒ (l : J) (fi : l âŸ¶ i) (fj : l âŸ¶ j) (fk : l âŸ¶ k)
      (Î± : F.obj l âŸ¶ pullback (V F i j).Î¹ (V F i k).Î¹) (z : F.obj l),
      IsOpenImmersion Î± âˆ§
      Î± â‰« pullback.fst _ _ = (F.map fi).isoOpensRange.hom â‰«
        (F.obj i).homOfLE (le_iSup_of_le âŸ¨l, _, fjâŸ© le_rfl) âˆ§
      Î± â‰« pullback.snd _ _ = (F.map fi).isoOpensRange.hom â‰«
        (F.obj i).homOfLE (le_iSup_of_le âŸ¨l, _, fkâŸ© le_rfl) âˆ§
      Î±.base z = x := by
  obtain âŸ¨kâ‚, yâ‚, hyâ‚âŸ© := mem_iSup.mp ((pullback.fst (C := Scheme) _ _).base x).2
  obtain âŸ¨kâ‚‚, yâ‚‚, hyâ‚‚âŸ© := mem_iSup.mp ((pullback.snd (C := Scheme) _ _).base x).2
  obtain âŸ¨l, hli, hlk, z, rfl, rflâŸ© :=
    (F â‹™ forget).exists_map_eq_of_isLocallyDirected kâ‚.2.1 kâ‚‚.2.1 yâ‚ yâ‚‚
      (by simpa [hyâ‚, hyâ‚‚] using congr($(pullback.condition (f := (V F i j).Î¹)).base x))
  let Î± : F.obj l âŸ¶ pullback (V F i j).Î¹ (V F i k).Î¹ :=
    pullback.lift
      ((F.map (hli â‰« kâ‚.2.1)).isoOpensRange.hom â‰« Scheme.homOfLE _
        (le_iSup_of_le âŸ¨l, hli â‰« kâ‚.2.1, hli â‰« kâ‚.2.2âŸ© le_rfl))
      ((F.map (hli â‰« kâ‚.2.1)).isoOpensRange.hom â‰« Scheme.homOfLE _
        (le_iSup_of_le âŸ¨l, hli â‰« kâ‚.2.1, hlk â‰« kâ‚‚.2.2âŸ© le_rfl))
      (by simp [â† cancel_mono (Scheme.Opens.Î¹ _)])
  have : IsOpenImmersion Î± := by
    apply (config := { allowSynthFailures := true }) IsOpenImmersion.of_comp
    Â· exact inferInstanceAs (IsOpenImmersion (pullback.fst _ _))
    Â· simp only [limit.lift_Ï€, PullbackCone.mk_pt, PullbackCone.mk_Ï€_app, Î±]
      infer_instance
  have : Î±.base z = x := by
    apply (pullback.fst (C := Scheme) _ _).isOpenEmbedding.injective
    apply (V F i j).Î¹.isOpenEmbedding.injective
    rw [â† Scheme.comp_base_apply, â† Scheme.comp_base_apply, pullback.lift_fst_assoc]
    simpa using hyâ‚
  exact âŸ¨l, hli â‰« kâ‚.2.1, hli â‰« kâ‚.2.2, hlk â‰« kâ‚‚.2.2, Î±, z, â€¹_â€º, by simp [Î±], by simp [Î±], â€¹_â€ºâŸ©

variable [Quiver.IsThin J]

lemma fst_inv_eq_snd_inv
    {i j : J} (kâ‚ kâ‚‚ : (k : J) Ã— (k âŸ¶ i) Ã— (k âŸ¶ j)) {U : (F.obj i).Opens}
    (hâ‚ : (F.map kâ‚.2.1).opensRange â‰¤ U) (hâ‚‚ : (F.map kâ‚‚.2.1).opensRange â‰¤ U) :
    pullback.fst ((F.obj i).homOfLE hâ‚) ((F.obj i).homOfLE hâ‚‚) â‰«
      (F.map kâ‚.2.1).isoOpensRange.inv â‰« F.map kâ‚.2.2 =
    pullback.snd ((F.obj i).homOfLE hâ‚) ((F.obj i).homOfLE hâ‚‚) â‰«
      (F.map kâ‚‚.2.1).isoOpensRange.inv â‰« F.map kâ‚‚.2.2 := by
  apply Scheme.hom_ext_of_forall
  intro x
  obtain âŸ¨l, hli, hlj, y, hyâ‚, hyâ‚‚âŸ© := (F â‹™ forget).exists_map_eq_of_isLocallyDirected kâ‚.2.1 kâ‚‚.2.1
    ((pullback.fst _ _ â‰« (F.map kâ‚.2.1).isoOpensRange.inv).base x)
    ((pullback.snd _ _ â‰« (F.map kâ‚‚.2.1).isoOpensRange.inv).base x) (by
      simp only [Functor.comp_obj, forget_obj, Functor.comp_map, forget_map, â† comp_base_apply,
        Category.assoc, Hom.isoOpensRange_inv_comp]
      congr 5
      simpa using congr($(pullback.condition (f := (F.obj i).homOfLE hâ‚)
        (g := (F.obj i).homOfLE hâ‚‚)) â‰« Scheme.Opens.Î¹ _))
  let Î± : F.obj l âŸ¶ pullback ((F.obj i).homOfLE hâ‚) ((F.obj i).homOfLE hâ‚‚) :=
    pullback.lift
      (F.map hli â‰« (F.map kâ‚.2.1).isoOpensRange.hom)
      (F.map hlj â‰« (F.map kâ‚‚.2.1).isoOpensRange.hom)
      (by simp [â† cancel_mono (Scheme.Opens.Î¹ _), â† Functor.map_comp,
        Subsingleton.elim (hli â‰« kâ‚.2.1) (hlj â‰« kâ‚‚.2.1)])
  have : IsOpenImmersion Î± := by
    have : IsOpenImmersion (Î± â‰« pullback.fst _ _) := by
      simp only [pullback.lift_fst, Î±]; infer_instance
    exact .of_comp _ (pullback.fst _ _)
  have : Î±.base y = x := by
    simp only [Functor.comp_obj, forget_obj, Functor.comp_map, forget_map, comp_coeBase,
      TopCat.hom_comp, ContinuousMap.comp_apply, Î±] at hyâ‚
    apply (pullback.fst ((F.obj i).homOfLE hâ‚) _).isOpenEmbedding.injective
    simp only [â† Scheme.comp_base_apply, Category.assoc, Î±, pullback.lift_fst]
    simp [hyâ‚]
  refine âŸ¨Î±.opensRange, âŸ¨y, thisâŸ©, ?_âŸ©
  rw [â† cancel_epi Î±.isoOpensRange.hom]
  simp [Î±, â† Functor.map_comp, Subsingleton.elim (hli â‰« kâ‚.2.2) (hlj â‰« kâ‚‚.2.2)]

/-- (Implemetation detail)
The inclusion map `V i j âŸ¶ F j` in the glue data associated to a locally directed diagram. -/
def tAux (i j : J) : (V F i j).toScheme âŸ¶ F.obj j :=
  (Scheme.Opens.iSupOpenCover _).glueMorphisms
    (fun k â†¦ (F.map k.2.1).isoOpensRange.inv â‰« F.map k.2.2) fun kâ‚ kâ‚‚ â†¦ by
      dsimp [Scheme.Opens.iSupOpenCover]
      apply fst_inv_eq_snd_inv F

@[reassoc]
lemma homOfLE_tAux (i j : J) {k : J} (fi : k âŸ¶ i) (fj : k âŸ¶ j) :
    (F.obj i).homOfLE (le_iSup_of_le âŸ¨k, fi, fjâŸ© le_rfl) â‰«
      tAux F i j = (F.map fi).isoOpensRange.inv â‰« F.map fj :=
  (Scheme.Opens.iSupOpenCover (J := Î£ k, (k âŸ¶ i) Ã— (k âŸ¶ j)) _).Î¹_glueMorphisms _ _ âŸ¨k, fi, fjâŸ©

/-- (Implemetation detail)
The transition map `V i j âŸ¶ V j i` in the glue data associated to a locally directed diagram. -/
def t (i j : J) : (V F i j).toScheme âŸ¶ (V F j i).toScheme :=
  IsOpenImmersion.lift (V F j i).Î¹ (tAux F i j) (by
    rintro _ âŸ¨x, rflâŸ©
    obtain âŸ¨l, x, rflâŸ© := (Scheme.Opens.iSupOpenCover _).exists_eq x
    simp only [V, tAux, â† Scheme.comp_base_apply, Category.assoc, Cover.Î¹_glueMorphisms]
    simp only [Opens.range_Î¹, iSup_mk, carrier_eq_coe, Hom.coe_opensRange, coe_mk, comp_coeBase,
      TopCat.hom_comp, ContinuousMap.comp_apply]
    exact Set.mem_iUnion.mpr âŸ¨âŸ¨l.1, l.2.2, l.2.1âŸ©, âŸ¨_, rflâŸ©âŸ©)

lemma t_id (i : J) : t F i i = ğŸ™ _ := by
  refine (Scheme.Opens.iSupOpenCover _).hom_ext _ _ fun k â†¦ ?_
  simp only [Category.comp_id, â† cancel_mono (Scheme.Opens.Î¹ _), Category.assoc,
    IsOpenImmersion.lift_fac, Scheme.Cover.Î¹_glueMorphisms, t, tAux, V]
  simp [Scheme.Opens.iSupOpenCover, Iso.inv_comp_eq, Subsingleton.elim k.2.1 k.2.2]

variable [Small.{u} J]

local notation3:max "â†“"j:arg => Equiv.symm (equivShrink _) j

/-- (Implemetation detail)
The glue data associated to a locally directed diagram.

One usually does not want to use this directly, and instead use the generic `colimit` API.
-/
def glueData : Scheme.GlueData where
  J := Shrink.{u} J
  U j := F.obj â†“j
  V ij := V F â†“ij.1 â†“ij.2
  f i j := Scheme.Opens.Î¹ _
  f_id i := V_self F â†“i â–¸ (Scheme.topIso _).isIso_hom
  f_hasPullback := inferInstance
  f_open := inferInstance
  t i j := t F â†“i â†“j
  t_id i := t_id F â†“i
  t' i j k := pullback.lift
    (IsOpenImmersion.lift (V F â†“j â†“k).Î¹ (pullback.fst _ _ â‰« tAux F â†“i â†“j) (by
      rintro _ âŸ¨x, rflâŸ©
      obtain âŸ¨l, fi, fj, fk, Î±, z, hÎ±, hÎ±â‚, hÎ±â‚‚, rflâŸ© := exists_of_pullback_V_V F x
      rw [â† Scheme.comp_base_apply, reassoc_of% hÎ±â‚, homOfLE_tAux F â†“i â†“j fi fj,
        Iso.hom_inv_id_assoc, Scheme.Opens.range_Î¹, SetLike.mem_coe]
      exact TopologicalSpace.Opens.mem_iSup.mpr âŸ¨âŸ¨l, fj, fkâŸ©, âŸ¨z, rflâŸ©âŸ©))
      (pullback.fst _ _ â‰« t F _ _) (by simp [t])
  t_fac i j k := pullback.lift_snd _ _ _
  cocycle i j k := by
    refine Scheme.hom_ext_of_forall _ _ fun x â†¦ ?_
    have := exists_of_pullback_V_V F x
    obtain âŸ¨l, fi, fj, fk, Î±, z, hÎ±, hÎ±â‚, hÎ±â‚‚, eâŸ© := this -- doing them in the same step times out.
    refine âŸ¨Î±.opensRange, âŸ¨_, eâŸ©, ?_âŸ©
    rw [â† cancel_mono (pullback.snd _ _), â† cancel_mono (Scheme.Opens.Î¹ _)]
    simp only [t, Category.assoc, limit.lift_Ï€, Scheme.comp_coeBase, TopCat.hom_comp,
      ContinuousMap.coe_comp, id_eq, PullbackCone.mk_pt, PullbackCone.mk_Ï€_app, limit.lift_Ï€_assoc,
      cospan_left, IsOpenImmersion.lift_fac, Category.id_comp]
    rw [IsOpenImmersion.comp_lift_assoc]
    simp only [limit.lift_Ï€_assoc, Scheme.comp_coeBase, TopCat.hom_comp, ContinuousMap.coe_comp,
      id_eq, PullbackCone.mk_pt, cospan_left, PullbackCone.mk_Ï€_app]
    rw [â† cancel_epi Î±.isoOpensRange.hom]
    simp_rw [Scheme.Hom.isoOpensRange_hom_Î¹_assoc, IsOpenImmersion.comp_lift_assoc]
    simp only [reassoc_of% hÎ±â‚, homOfLE_tAux F _ _ fi fj, Iso.hom_inv_id_assoc, reassoc_of% hÎ±â‚‚]
    generalize_proofs _ hâ‚
    have : IsOpenImmersion.lift (V F â†“j â†“k).Î¹ (F.map fj) hâ‚ = (F.map fj).isoOpensRange.hom â‰«
        (F.obj â†“j).homOfLE (le_iSup_of_le âŸ¨l, fj, fkâŸ© le_rfl) := by
      rw [â† cancel_mono (Scheme.Opens.Î¹ _), Category.assoc, IsOpenImmersion.lift_fac,
        â† Iso.inv_comp_eq, Scheme.Hom.isoOpensRange_inv_comp]
      exact (Scheme.homOfLE_Î¹ _ _).symm
    simp_rw [this, Category.assoc, homOfLE_tAux F _ _ fj fk, Iso.hom_inv_id_assoc]
    generalize_proofs hâ‚‚
    have : IsOpenImmersion.lift (V F â†“k â†“i).Î¹ (F.map fk) hâ‚‚ = (F.map fk).isoOpensRange.hom â‰«
        (F.obj â†“k).homOfLE (le_iSup_of_le âŸ¨l, fk, fiâŸ© le_rfl) := by
      rw [â† cancel_mono (Scheme.Opens.Î¹ _), Category.assoc, IsOpenImmersion.lift_fac,
        â† Iso.inv_comp_eq, Scheme.Hom.isoOpensRange_inv_comp]
      exact (Scheme.homOfLE_Î¹ _ _).symm
    simp_rw [this, Category.assoc, homOfLE_tAux F _ _ fk fi, Iso.hom_inv_id_assoc,
      â† Iso.inv_comp_eq, Scheme.Hom.isoOpensRange_inv_comp]
    exact (Scheme.homOfLE_Î¹ _ _).symm

lemma glueDataÎ¹_naturality {i j : Shrink.{u} J} (f : â†“i âŸ¶ â†“j) :
    F.map f â‰« (glueData F).Î¹ j = (glueData F).Î¹ i := by
  have : IsIso (V F â†“i â†“j).Î¹ := by
    have : V F â†“i â†“j = âŠ¤ :=
      top_le_iff.mp (le_iSup_of_le âŸ¨_, ğŸ™ i, fâŸ© (by simp [Scheme.Hom.opensRange_of_isIso]))
    exact this â–¸ (topIso _).isIso_hom
  have : t F â†“i â†“j â‰« (V F â†“j â†“i).Î¹ â‰« _ = (V F â†“i â†“j).Î¹ â‰« _ :=
    (glueData F).glue_condition i j
  simp only [t, IsOpenImmersion.lift_fac_assoc] at this
  rw [â† cancel_epi (V F â†“i â†“j).Î¹, â† this, â† Category.assoc,
    â† (Iso.eq_inv_comp _).mp (homOfLE_tAux F â†“i â†“j (ğŸ™ i) f),
    â† Category.assoc, â† Category.assoc, Category.assoc]
  convert Category.id_comp _
  rw [â† cancel_mono (Opens.Î¹ _)]
  simp [V, InducedCategory.category, Shrink.instCategoryShrink]

/-- (Implemetation detail)
The cocone associated to a locally directed diagram.

One usually does not want to use this directly, and instead use the generic `colimit` API.
-/
def cocone : Cocone F where
  pt := (glueData F).glued
  Î¹.app j := F.map (eqToHom (by simp)) â‰« (glueData F).Î¹ (equivShrink _ j)
  Î¹.naturality {i j} f := by
    simp only [Category.assoc, â† IsIso.inv_comp_eq, â† Functor.map_inv, â† Functor.map_comp_assoc,
      glueDataÎ¹_naturality, Functor.const_obj_obj, Functor.const_obj_map, Category.comp_id]

/-- (Implemetation detail)
The cocone associated to a locally directed diagram is a colimit.

One usually does not want to use this directly, and instead use the generic `colimit` API.
-/
noncomputable
def isColimit : IsColimit (cocone F) where
  desc s := Multicoequalizer.desc _ _ (fun i â†¦ s.Î¹.app â†“i) (by
    rintro âŸ¨i, jâŸ©
    dsimp [glueData, GlueData.diagram]
    simp only [t, IsOpenImmersion.lift_fac]
    apply (Scheme.Opens.iSupOpenCover _).hom_ext _ _ fun k â†¦ ?_
    simp only [Opens.iSupOpenCover, V, Scheme.homOfLE_Î¹_assoc]
    rw [homOfLE_tAux_assoc F â†“i â†“j k.2.1 k.2.2, Iso.eq_inv_comp]
    simp)
  fac s j := by
    refine (Category.assoc _ _ _).trans ?_
    conv_lhs => enter [2]; tactic => exact Multicoequalizer.Ï€_desc _ _ _ _ _
    simp
  uniq s m hm := Multicoequalizer.hom_ext _ _ _ fun i â†¦ by
    simp [â† hm â†“i, cocone, reassoc_of% glueDataÎ¹_naturality]
    rfl

/-- (Implemetation detail)
The cocone associated to a locally directed diagram is a colimit as locally ringed spaces.

One usually does not want to use this directly, and instead use the generic `colimit` API.
-/
noncomputable
def isColimitForgetToLocallyRingedSpace :
    IsColimit (Scheme.forgetToLocallyRingedSpace.mapCocone (cocone F)) where
  desc s := (glueData F).isoLocallyRingedSpace.hom â‰«
    Multicoequalizer.desc _ _ (fun i â†¦ s.Î¹.app â†“i) (by
      rintro âŸ¨i, jâŸ©
      dsimp [glueData, GlueData.diagram]
      simp only [t, IsOpenImmersion.lift_fac, â† Category.assoc, â† Scheme.comp_toLRSHom]
      rw [â† cancel_epi (Scheme.Opens.iSupOpenCover _).ulift.fromGlued.toLRSHom,
        â† cancel_epi (Scheme.Opens.iSupOpenCover _).ulift.gluedCover.isoLocallyRingedSpace.inv]
      refine Multicoequalizer.hom_ext _ _ _ fun âŸ¨k, hkâŸ© â†¦ ?_
      rw [â† CategoryTheory.GlueData.Î¹, reassoc_of% GlueData.Î¹_isoLocallyRingedSpace_inv,
        reassoc_of% GlueData.Î¹_isoLocallyRingedSpace_inv,
        â† cancel_epi (Hom.isoOpensRange (F.map _)).hom.toLRSHom]
      simp only [Opens.iSupOpenCover, Cover.ulift, V, â† comp_toLRSHom_assoc,
        Cover.Î¹_fromGlued_assoc, homOfLE_Î¹, Hom.isoOpensRange_hom_Î¹]
      generalize_proofs _ h
      rw [homOfLE_tAux F â†“i â†“j h.choose.2.1 h.choose.2.2, Iso.hom_inv_id_assoc]
      exact (s.w h.choose.2.1).trans (s.w h.choose.2.2).symm)
  fac s j := by
    simp only [cocone, Functor.mapCocone_Î¹_app, Scheme.comp_toLRSHom,
      forgetToLocallyRingedSpace_map, â† GlueData.Î¹_isoLocallyRingedSpace_inv]
    simpa [CategoryTheory.GlueData.Î¹] using s.w _
  uniq s m hm := by
    rw [â† Iso.inv_comp_eq]
    refine Multicoequalizer.hom_ext _ _ _ fun i â†¦ ?_
    conv_lhs => rw [â† Î¹.eq_def]
    dsimp
    simp [cocone, â† hm, glueDataÎ¹_naturality,
      â† GlueData.Î¹_isoLocallyRingedSpace_inv, -Î¹_gluedIso_inv_assoc, -Î¹_gluedIso_inv]

instance : HasColimit F := âŸ¨_, isColimit FâŸ©

instance : PreservesColimit F Scheme.forgetToLocallyRingedSpace :=
  preservesColimit_of_preserves_colimit_cocone (isColimit F) (isColimitForgetToLocallyRingedSpace F)

instance : CreatesColimit F Scheme.forgetToLocallyRingedSpace :=
  CategoryTheory.createsColimitOfReflectsIsomorphismsOfPreserves

/-- The open cover of the colimit of a locally directed diagram by the components. -/
def openCover : (colimit F).OpenCover :=
  ((coverOfIsIso ((isColimit F).coconePointUniqueUpToIso (colimit.isColimit F)).hom).bind
    fun i â†¦ (glueData F).openCover).copy J F.obj (colimit.Î¹ F)
    ((equivShrink J).trans <| (Equiv.uniqueSigma fun (_ : Unit) â†¦ Shrink J).symm)
    (fun _ â†¦ F.mapIso (eqToIso (by simp [GlueData.openCover, glueData]))) fun i â†¦ by
  show colimit.Î¹ F i = _ â‰« (glueData F).Î¹ (equivShrink J i) â‰« _
  simp [â† Category.assoc, â† Iso.comp_inv_eq, cocone]

@[simp] lemma openCover_J : (openCover F).J = J := rfl
@[simp] lemma openCover_obj : (openCover F).obj = F.obj := rfl
@[simp] lemma openCover_map : (openCover F).map = colimit.Î¹ F := rfl

instance (i) : IsOpenImmersion (colimit.Î¹ F i) :=
  inferInstanceAs (IsOpenImmersion ((openCover F).map i))

lemma Î¹_eq_Î¹_iff {i j : J} {xi : F.obj i} {xj : F.obj j} :
    (colimit.Î¹ F i).base xi = (colimit.Î¹ F j).base xj â†”
      âˆƒ k fi fj, âˆƒ (x : F.obj k), (F.map fi).base x = xi âˆ§ (F.map fj).base x = xj := by
  constructor; swap
  Â· rintro âŸ¨k, fi, fj, x, rfl, rflâŸ©; simp only [â† Scheme.comp_base_apply, colimit.w]
  obtain âŸ¨i, rflâŸ© := (equivShrink J).symm.surjective i
  obtain âŸ¨j, rflâŸ© := (equivShrink J).symm.surjective j
  rw [â† ((isColimit F).coconePointUniqueUpToIso
    (colimit.isColimit F)).inv.isOpenEmbedding.injective.eq_iff]
  simp only [Limits.colimit, â† Scheme.comp_base_apply,
    colimit.comp_coconePointUniqueUpToIso_inv, cocone, glueDataÎ¹_naturality]
  refine ?_ âˆ˜ ((glueData F).Î¹_eq_iff _ _ _ _).mp
  dsimp only [GlueData.Rel]
  rintro âŸ¨x, rfl, rflâŸ©
  obtain âŸ¨âŸ¨k, ki, kjâŸ©, y, hy : (F.map ki).base y = ((glueData F).f i j).base xâŸ© := mem_iSup.mp x.2
  refine âŸ¨k, ki, kj, y, hy, ?_âŸ©
  obtain âŸ¨k, rflâŸ© := (equivShrink J).symm.surjective k
  apply ((glueData F).Î¹ _).isOpenEmbedding.injective
  simp only [â† Scheme.comp_base_apply, Category.assoc, GlueData.glue_condition]
  trans ((glueData F).Î¹ k).base y
  Â· simp [â† glueDataÎ¹_naturality F kj]; rfl
  Â· simp [â† glueDataÎ¹_naturality F ki, â† hy]; rfl

instance (F : WidePushoutShape J â¥¤ Scheme.{u}) [âˆ€ {i j} (f : i âŸ¶ j), IsOpenImmersion (F.map f)] :
    (F â‹™ forget).IsLocallyDirected :=
  have (i) : Mono ((F â‹™ forget).map (.init i)) :=
    (mono_iff_injective _).mpr (F.map _).isOpenEmbedding.injective
  inferInstance

end IsLocallyDirected

end IsLocallyDirected

end Scheme

end AlgebraicGeometry
