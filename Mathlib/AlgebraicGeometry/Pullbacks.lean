/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Gluing
import Mathlib.CategoryTheory.Limits.Opposites
import Mathlib.AlgebraicGeometry.AffineScheme
import Mathlib.CategoryTheory.Limits.Shapes.Diagonal

#align_import algebraic_geometry.pullbacks from "leanprover-community/mathlib"@"7316286ff2942aa14e540add9058c6b0aa1c8070"

/-!
# Fibred products of schemes

In this file we construct the fibred product of schemes via gluing.
We roughly follow [har77] Theorem 3.3.

In particular, the main construction is to show that for an open cover `{ Uáµ¢ }` of `X`, if there
exist fibred products `Uáµ¢ Ã—[Z] Y` for each `i`, then there exists a fibred product `X Ã—[Z] Y`.

Then, for constructing the fibred product for arbitrary schemes `X, Y, Z`, we can use the
construction to reduce to the case where `X, Y, Z` are all affine, where fibred products are
constructed via tensor products.

-/

set_option linter.uppercaseLean3 false

universe v u

noncomputable section

open CategoryTheory CategoryTheory.Limits AlgebraicGeometry

namespace AlgebraicGeometry.Scheme

namespace Pullback

variable {C : Type u} [Category.{v} C]
variable {X Y Z : Scheme.{u}} (ğ’° : OpenCover.{u} X) (f : X âŸ¶ Z) (g : Y âŸ¶ Z)
variable [âˆ€ i, HasPullback (ğ’°.map i â‰« f) g]

/-- The intersection of `Uáµ¢ Ã—[Z] Y` and `Uâ±¼ Ã—[Z] Y` is given by (Uáµ¢ Ã—[Z] Y) Ã—[X] Uâ±¼ -/
def v (i j : ğ’°.J) : Scheme :=
  pullback ((pullback.fst : pullback (ğ’°.map i â‰« f) g âŸ¶ _) â‰« ğ’°.map i) (ğ’°.map j)
#align algebraic_geometry.Scheme.pullback.V AlgebraicGeometry.Scheme.Pullback.v

/-- The canonical transition map `(Uáµ¢ Ã—[Z] Y) Ã—[X] Uâ±¼ âŸ¶ (Uâ±¼ Ã—[Z] Y) Ã—[X] Uáµ¢` given by the fact
that pullbacks are associative and symmetric. -/
def t (i j : ğ’°.J) : v ğ’° f g i j âŸ¶ v ğ’° f g j i := by
  haveI : HasPullback (pullback.snd â‰« ğ’°.map i â‰« f) g :=
    hasPullback_assoc_symm (ğ’°.map j) (ğ’°.map i) (ğ’°.map i â‰« f) g
  haveI : HasPullback (pullback.snd â‰« ğ’°.map j â‰« f) g :=
    hasPullback_assoc_symm (ğ’°.map i) (ğ’°.map j) (ğ’°.map j â‰« f) g
  refine' (pullbackSymmetry _ _).hom â‰« _
  refine' (pullbackAssoc _ _ _ _).inv â‰« _
  change pullback _ _ âŸ¶ pullback _ _
  refine' _ â‰« (pullbackSymmetry _ _).hom
  refine' _ â‰« (pullbackAssoc _ _ _ _).hom
  refine' pullback.map _ _ _ _ (pullbackSymmetry _ _).hom (ğŸ™ _) (ğŸ™ _) _ _
  rw [pullbackSymmetry_hom_comp_snd_assoc, pullback.condition_assoc, Category.comp_id]
  rw [Category.comp_id, Category.id_comp]
#align algebraic_geometry.Scheme.pullback.t AlgebraicGeometry.Scheme.Pullback.t

@[simp, reassoc]
theorem t_fst_fst (i j : ğ’°.J) : t ğ’° f g i j â‰« pullback.fst â‰« pullback.fst = pullback.snd := by
  delta t
  -- Porting note: copied from previous definition, otherwise `simp` wouldn't work
  haveI : HasPullback (pullback.snd â‰« ğ’°.map i â‰« f) g :=
    hasPullback_assoc_symm (ğ’°.map j) (ğ’°.map i) (ğ’°.map i â‰« f) g
  haveI : HasPullback (pullback.snd â‰« ğ’°.map j â‰« f) g :=
    hasPullback_assoc_symm (ğ’°.map i) (ğ’°.map j) (ğ’°.map j â‰« f) g
  simp only [Category.assoc, id.def, pullbackSymmetry_hom_comp_fst_assoc,
    pullbackAssoc_hom_snd_fst, pullback.lift_fst_assoc, pullbackSymmetry_hom_comp_snd,
    pullbackAssoc_inv_fst_fst, pullbackSymmetry_hom_comp_fst]
#align algebraic_geometry.Scheme.pullback.t_fst_fst AlgebraicGeometry.Scheme.Pullback.t_fst_fst

@[simp, reassoc]
theorem t_fst_snd (i j : ğ’°.J) :
    t ğ’° f g i j â‰« pullback.fst â‰« pullback.snd = pullback.fst â‰« pullback.snd := by
  delta t
  -- Porting note: copied from previous definition, otherwise `simp` wouldn't work
  haveI : HasPullback (pullback.snd â‰« ğ’°.map i â‰« f) g :=
    hasPullback_assoc_symm (ğ’°.map j) (ğ’°.map i) (ğ’°.map i â‰« f) g
  haveI : HasPullback (pullback.snd â‰« ğ’°.map j â‰« f) g :=
    hasPullback_assoc_symm (ğ’°.map i) (ğ’°.map j) (ğ’°.map j â‰« f) g
  simp only [pullbackSymmetry_hom_comp_snd_assoc, Category.comp_id, Category.assoc, id.def,
    pullbackSymmetry_hom_comp_fst_assoc, pullbackAssoc_hom_snd_snd, pullback.lift_snd,
    pullbackAssoc_inv_snd]
#align algebraic_geometry.Scheme.pullback.t_fst_snd AlgebraicGeometry.Scheme.Pullback.t_fst_snd

@[simp, reassoc]
theorem t_snd (i j : ğ’°.J) : t ğ’° f g i j â‰« pullback.snd = pullback.fst â‰« pullback.fst := by
  delta t
  -- Porting note: copied from previous definition, otherwise `simp` wouldn't work
  haveI : HasPullback (pullback.snd â‰« ğ’°.map i â‰« f) g :=
    hasPullback_assoc_symm (ğ’°.map j) (ğ’°.map i) (ğ’°.map i â‰« f) g
  haveI : HasPullback (pullback.snd â‰« ğ’°.map j â‰« f) g :=
    hasPullback_assoc_symm (ğ’°.map i) (ğ’°.map j) (ğ’°.map j â‰« f) g
  simp only [pullbackSymmetry_hom_comp_snd_assoc, Category.assoc, id.def,
    pullbackSymmetry_hom_comp_snd, pullbackAssoc_hom_fst, pullback.lift_fst_assoc,
    pullbackSymmetry_hom_comp_fst, pullbackAssoc_inv_fst_snd]
#align algebraic_geometry.Scheme.pullback.t_snd AlgebraicGeometry.Scheme.Pullback.t_snd

theorem t_id (i : ğ’°.J) : t ğ’° f g i i = ğŸ™ _ := by
  apply pullback.hom_ext <;> rw [Category.id_comp]
  apply pullback.hom_ext
  Â· rw [â† cancel_mono (ğ’°.map i)]; simp only [pullback.condition, Category.assoc, t_fst_fst]
  Â· simp only [Category.assoc, t_fst_snd]
  Â· rw [â† cancel_mono (ğ’°.map i)]; simp only [pullback.condition, t_snd, Category.assoc]
#align algebraic_geometry.Scheme.pullback.t_id AlgebraicGeometry.Scheme.Pullback.t_id

/-- The inclusion map of `V i j = (Uáµ¢ Ã—[Z] Y) Ã—[X] Uâ±¼ âŸ¶ Uáµ¢ Ã—[Z] Y`-/
abbrev fV (i j : ğ’°.J) : v ğ’° f g i j âŸ¶ pullback (ğ’°.map i â‰« f) g :=
  pullback.fst
#align algebraic_geometry.Scheme.pullback.fV AlgebraicGeometry.Scheme.Pullback.fV

/-- The map `((Xáµ¢ Ã—[Z] Y) Ã—[X] Xâ±¼) Ã—[Xáµ¢ Ã—[Z] Y] ((Xáµ¢ Ã—[Z] Y) Ã—[X] Xâ‚–)` âŸ¶
  `((Xâ±¼ Ã—[Z] Y) Ã—[X] Xâ‚–) Ã—[Xâ±¼ Ã—[Z] Y] ((Xâ±¼ Ã—[Z] Y) Ã—[X] Xáµ¢)` needed for gluing   -/
def t' (i j k : ğ’°.J) :
    pullback (fV ğ’° f g i j) (fV ğ’° f g i k) âŸ¶ pullback (fV ğ’° f g j k) (fV ğ’° f g j i) := by
  refine' (pullbackRightPullbackFstIso _ _ _).hom â‰« _
  refine' _ â‰« (pullbackSymmetry _ _).hom
  refine' _ â‰« (pullbackRightPullbackFstIso _ _ _).inv
  refine' pullback.map _ _ _ _ (t ğ’° f g i j) (ğŸ™ _) (ğŸ™ _) _ _
  Â· simp only [â† pullback.condition, Category.comp_id, t_fst_fst_assoc]
  Â· simp only [Category.comp_id, Category.id_comp]
#align algebraic_geometry.Scheme.pullback.t' AlgebraicGeometry.Scheme.Pullback.t'

@[simp, reassoc]
theorem t'_fst_fst_fst (i j k : ğ’°.J) :
    t' ğ’° f g i j k â‰« pullback.fst â‰« pullback.fst â‰« pullback.fst = pullback.fst â‰« pullback.snd := by
  delta t'
  simp only [Category.assoc, pullbackSymmetry_hom_comp_fst_assoc,
    pullbackRightPullbackFstIso_inv_snd_fst_assoc, pullback.lift_fst_assoc, t_fst_fst,
    pullbackRightPullbackFstIso_hom_fst_assoc]
#align algebraic_geometry.Scheme.pullback.t'_fst_fst_fst AlgebraicGeometry.Scheme.Pullback.t'_fst_fst_fst

@[simp, reassoc]
theorem t'_fst_fst_snd (i j k : ğ’°.J) :
    t' ğ’° f g i j k â‰« pullback.fst â‰« pullback.fst â‰« pullback.snd =
      pullback.fst â‰« pullback.fst â‰« pullback.snd := by
  delta t'
  simp only [Category.assoc, pullbackSymmetry_hom_comp_fst_assoc,
    pullbackRightPullbackFstIso_inv_snd_fst_assoc, pullback.lift_fst_assoc, t_fst_snd,
    pullbackRightPullbackFstIso_hom_fst_assoc]
#align algebraic_geometry.Scheme.pullback.t'_fst_fst_snd AlgebraicGeometry.Scheme.Pullback.t'_fst_fst_snd

@[simp, reassoc]
theorem t'_fst_snd (i j k : ğ’°.J) :
    t' ğ’° f g i j k â‰« pullback.fst â‰« pullback.snd = pullback.snd â‰« pullback.snd := by
  delta t'
  simp only [Category.comp_id, Category.assoc, pullbackSymmetry_hom_comp_fst_assoc,
    pullbackRightPullbackFstIso_inv_snd_snd, pullback.lift_snd,
    pullbackRightPullbackFstIso_hom_snd]
#align algebraic_geometry.Scheme.pullback.t'_fst_snd AlgebraicGeometry.Scheme.Pullback.t'_fst_snd

@[simp, reassoc]
theorem t'_snd_fst_fst (i j k : ğ’°.J) :
    t' ğ’° f g i j k â‰« pullback.snd â‰« pullback.fst â‰« pullback.fst = pullback.fst â‰« pullback.snd := by
  delta t'
  simp only [Category.assoc, pullbackSymmetry_hom_comp_snd_assoc,
    pullbackRightPullbackFstIso_inv_fst_assoc, pullback.lift_fst_assoc, t_fst_fst,
    pullbackRightPullbackFstIso_hom_fst_assoc]
#align algebraic_geometry.Scheme.pullback.t'_snd_fst_fst AlgebraicGeometry.Scheme.Pullback.t'_snd_fst_fst

@[simp, reassoc]
theorem t'_snd_fst_snd (i j k : ğ’°.J) :
    t' ğ’° f g i j k â‰« pullback.snd â‰« pullback.fst â‰« pullback.snd =
      pullback.fst â‰« pullback.fst â‰« pullback.snd := by
  delta t'
  simp only [Category.assoc, pullbackSymmetry_hom_comp_snd_assoc,
    pullbackRightPullbackFstIso_inv_fst_assoc, pullback.lift_fst_assoc, t_fst_snd,
    pullbackRightPullbackFstIso_hom_fst_assoc]
#align algebraic_geometry.Scheme.pullback.t'_snd_fst_snd AlgebraicGeometry.Scheme.Pullback.t'_snd_fst_snd

@[simp, reassoc]
theorem t'_snd_snd (i j k : ğ’°.J) :
    t' ğ’° f g i j k â‰« pullback.snd â‰« pullback.snd = pullback.fst â‰« pullback.fst â‰« pullback.fst := by
  delta t'
  simp only [Category.assoc, pullbackSymmetry_hom_comp_snd_assoc,
    pullbackRightPullbackFstIso_inv_fst_assoc, pullback.lift_fst_assoc, t_snd,
    pullbackRightPullbackFstIso_hom_fst_assoc]
#align algebraic_geometry.Scheme.pullback.t'_snd_snd AlgebraicGeometry.Scheme.Pullback.t'_snd_snd

theorem cocycle_fst_fst_fst (i j k : ğ’°.J) :
    t' ğ’° f g i j k â‰« t' ğ’° f g j k i â‰« t' ğ’° f g k i j â‰« pullback.fst â‰« pullback.fst â‰« pullback.fst =
      pullback.fst â‰« pullback.fst â‰« pullback.fst :=
  by simp only [t'_fst_fst_fst, t'_fst_snd, t'_snd_snd]
#align algebraic_geometry.Scheme.pullback.cocycle_fst_fst_fst AlgebraicGeometry.Scheme.Pullback.cocycle_fst_fst_fst

theorem cocycle_fst_fst_snd (i j k : ğ’°.J) :
    t' ğ’° f g i j k â‰« t' ğ’° f g j k i â‰« t' ğ’° f g k i j â‰« pullback.fst â‰« pullback.fst â‰« pullback.snd =
      pullback.fst â‰« pullback.fst â‰« pullback.snd :=
  by simp only [t'_fst_fst_snd]
#align algebraic_geometry.Scheme.pullback.cocycle_fst_fst_snd AlgebraicGeometry.Scheme.Pullback.cocycle_fst_fst_snd

theorem cocycle_fst_snd (i j k : ğ’°.J) :
    t' ğ’° f g i j k â‰« t' ğ’° f g j k i â‰« t' ğ’° f g k i j â‰« pullback.fst â‰« pullback.snd =
      pullback.fst â‰« pullback.snd :=
  by simp only [t'_fst_snd, t'_snd_snd, t'_fst_fst_fst]
#align algebraic_geometry.Scheme.pullback.cocycle_fst_snd AlgebraicGeometry.Scheme.Pullback.cocycle_fst_snd

theorem cocycle_snd_fst_fst (i j k : ğ’°.J) :
    t' ğ’° f g i j k â‰« t' ğ’° f g j k i â‰« t' ğ’° f g k i j â‰« pullback.snd â‰« pullback.fst â‰« pullback.fst =
      pullback.snd â‰« pullback.fst â‰« pullback.fst := by
  rw [â† cancel_mono (ğ’°.map i)]
  simp only [pullback.condition_assoc, t'_snd_fst_fst, t'_fst_snd, t'_snd_snd]
#align algebraic_geometry.Scheme.pullback.cocycle_snd_fst_fst AlgebraicGeometry.Scheme.Pullback.cocycle_snd_fst_fst

theorem cocycle_snd_fst_snd (i j k : ğ’°.J) :
    t' ğ’° f g i j k â‰« t' ğ’° f g j k i â‰« t' ğ’° f g k i j â‰« pullback.snd â‰« pullback.fst â‰« pullback.snd =
      pullback.snd â‰« pullback.fst â‰« pullback.snd :=
  by simp only [pullback.condition_assoc, t'_snd_fst_snd]
#align algebraic_geometry.Scheme.pullback.cocycle_snd_fst_snd AlgebraicGeometry.Scheme.Pullback.cocycle_snd_fst_snd

theorem cocycle_snd_snd (i j k : ğ’°.J) :
    t' ğ’° f g i j k â‰« t' ğ’° f g j k i â‰« t' ğ’° f g k i j â‰« pullback.snd â‰« pullback.snd =
      pullback.snd â‰« pullback.snd :=
  by simp only [t'_snd_snd, t'_fst_fst_fst, t'_fst_snd]
#align algebraic_geometry.Scheme.pullback.cocycle_snd_snd AlgebraicGeometry.Scheme.Pullback.cocycle_snd_snd

-- `by tidy` should solve it, but it times out.
theorem cocycle (i j k : ğ’°.J) : t' ğ’° f g i j k â‰« t' ğ’° f g j k i â‰« t' ğ’° f g k i j = ğŸ™ _ := by
  apply pullback.hom_ext <;> rw [Category.id_comp]
  Â· apply pullback.hom_ext
    Â· apply pullback.hom_ext
      Â· simp_rw [Category.assoc]
        exact cocycle_fst_fst_fst ğ’° f g i j k
      Â· simp_rw [Category.assoc]
        exact cocycle_fst_fst_snd ğ’° f g i j k
    Â· simp_rw [Category.assoc]
      exact cocycle_fst_snd ğ’° f g i j k
  Â· apply pullback.hom_ext
    Â· apply pullback.hom_ext
      Â· simp_rw [Category.assoc]
        exact cocycle_snd_fst_fst ğ’° f g i j k
      Â· simp_rw [Category.assoc]
        exact cocycle_snd_fst_snd ğ’° f g i j k
    Â· simp_rw [Category.assoc]
      exact cocycle_snd_snd ğ’° f g i j k
#align algebraic_geometry.Scheme.pullback.cocycle AlgebraicGeometry.Scheme.Pullback.cocycle

/-- Given `Uáµ¢ Ã—[Z] Y`, this is the glued fibered product `X Ã—[Z] Y`. -/
@[simps]
def gluing : Scheme.GlueData.{u} where
  J := ğ’°.J
  U i := pullback (ğ’°.map i â‰« f) g
  V := fun âŸ¨i, jâŸ© => v ğ’° f g i j
  -- `pâ»Â¹(Uáµ¢ âˆ© Uâ±¼)` where `p : Uáµ¢ Ã—[Z] Y âŸ¶ Uáµ¢ âŸ¶ X`.
  f i j := pullback.fst
  f_id i := inferInstance
  f_open := inferInstance
  t i j := t ğ’° f g i j
  t_id i := t_id ğ’° f g i
  t' i j k := t' ğ’° f g i j k
  t_fac i j k := by
    apply pullback.hom_ext
    apply pullback.hom_ext
    all_goals
      simp only [t'_snd_fst_fst, t'_snd_fst_snd, t'_snd_snd, t_fst_fst, t_fst_snd, t_snd,
        Category.assoc]
  cocycle i j k := cocycle ğ’° f g i j k
#align algebraic_geometry.Scheme.pullback.gluing AlgebraicGeometry.Scheme.Pullback.gluing

/-- The first projection from the glued scheme into `X`. -/
def p1 : (gluing ğ’° f g).glued âŸ¶ X := by
  fapply Multicoequalizer.desc
  exact fun i => pullback.fst â‰« ğ’°.map i
  rintro âŸ¨i, jâŸ©
  change pullback.fst â‰« _ â‰« ğ’°.map i = (_ â‰« _) â‰« _ â‰« ğ’°.map j
  -- Porting note (#11224): change `rw` to `erw`
  erw [pullback.condition]
  rw [â† Category.assoc]
  congr 1
  rw [Category.assoc]
  exact (t_fst_fst _ _ _ _ _).symm
#align algebraic_geometry.Scheme.pullback.p1 AlgebraicGeometry.Scheme.Pullback.p1

/-- The second projection from the glued scheme into `Y`. -/
def p2 : (gluing ğ’° f g).glued âŸ¶ Y := by
  fapply Multicoequalizer.desc
  exact fun i => pullback.snd
  rintro âŸ¨i, jâŸ©
  change pullback.fst â‰« _ = (_ â‰« _) â‰« _
  rw [Category.assoc]
  exact (t_fst_snd _ _ _ _ _).symm
#align algebraic_geometry.Scheme.pullback.p2 AlgebraicGeometry.Scheme.Pullback.p2

theorem p_comm : p1 ğ’° f g â‰« f = p2 ğ’° f g â‰« g := by
  apply Multicoequalizer.hom_ext
  intro i
  erw [Multicoequalizer.Ï€_desc_assoc, Multicoequalizer.Ï€_desc_assoc]
  rw [Category.assoc, pullback.condition]
#align algebraic_geometry.Scheme.pullback.p_comm AlgebraicGeometry.Scheme.Pullback.p_comm

variable (s : PullbackCone f g)

/-- (Implementation)
The canonical map `(s.X Ã—[X] Uáµ¢) Ã—[s.X] (s.X Ã—[X] Uâ±¼) âŸ¶ (Uáµ¢ Ã—[Z] Y) Ã—[X] Uâ±¼`

This is used in `gluedLift`. -/
def gluedLiftPullbackMap (i j : ğ’°.J) :
    pullback ((ğ’°.pullbackCover s.fst).map i) ((ğ’°.pullbackCover s.fst).map j) âŸ¶
      (gluing ğ’° f g).V âŸ¨i, jâŸ© := by
  change pullback pullback.fst pullback.fst âŸ¶ pullback _ _
  refine' (pullbackRightPullbackFstIso _ _ _).hom â‰« _
  refine' pullback.map _ _ _ _ _ (ğŸ™ _) (ğŸ™ _) _ _
  Â· exact (pullbackSymmetry _ _).hom â‰«
      pullback.map _ _ _ _ (ğŸ™ _) s.snd f (Category.id_comp _).symm s.condition
  Â· simpa using pullback.condition
  Â· simp only [Category.comp_id, Category.id_comp]
#align algebraic_geometry.Scheme.pullback.glued_lift_pullback_map AlgebraicGeometry.Scheme.Pullback.gluedLiftPullbackMap

@[reassoc]
theorem gluedLiftPullbackMap_fst (i j : ğ’°.J) :
    gluedLiftPullbackMap ğ’° f g s i j â‰« pullback.fst =
      pullback.fst â‰«
        (pullbackSymmetry _ _).hom â‰«
          pullback.map _ _ _ _ (ğŸ™ _) s.snd f (Category.id_comp _).symm s.condition := by
  delta gluedLiftPullbackMap
  -- Porting note: the original set of simp lemma is not sufficient, but as this is terminal
  -- I just let `simp` do its work
  simp
#align algebraic_geometry.Scheme.pullback.glued_lift_pullback_map_fst AlgebraicGeometry.Scheme.Pullback.gluedLiftPullbackMap_fst

@[reassoc]
theorem gluedLiftPullbackMap_snd (i j : ğ’°.J) :
    gluedLiftPullbackMap ğ’° f g s i j â‰« pullback.snd = pullback.snd â‰« pullback.snd := by
  delta gluedLiftPullbackMap
  -- Porting note: the original set of simp lemma is not sufficient, but as this is terminal
  -- I just let `simp` do its work
  simp
#align algebraic_geometry.Scheme.pullback.glued_lift_pullback_map_snd AlgebraicGeometry.Scheme.Pullback.gluedLiftPullbackMap_snd

/-- The lifted map `s.X âŸ¶ (gluing ğ’° f g).glued` in order to show that `(gluing ğ’° f g).glued` is
indeed the pullback.

Given a pullback cone `s`, we have the maps `s.fst â»Â¹' Uáµ¢ âŸ¶ Uáµ¢` and
`s.fst â»Â¹' Uáµ¢ âŸ¶ s.X âŸ¶ Y` that we may lift to a map `s.fst â»Â¹' Uáµ¢ âŸ¶ Uáµ¢ Ã—[Z] Y`.

to glue these into a map `s.X âŸ¶ Uáµ¢ Ã—[Z] Y`, we need to show that the maps agree on
`(s.fst â»Â¹' Uáµ¢) Ã—[s.X] (s.fst â»Â¹' Uâ±¼) âŸ¶ Uáµ¢ Ã—[Z] Y`. This is achieved by showing that both of these
maps factors through `gluedLiftPullbackMap`.
-/
def gluedLift : s.pt âŸ¶ (gluing ğ’° f g).glued := by
  fapply (ğ’°.pullbackCover s.fst).glueMorphisms
  Â· exact fun i => (pullbackSymmetry _ _).hom â‰«
      pullback.map _ _ _ _ (ğŸ™ _) s.snd f (Category.id_comp _).symm s.condition â‰«
        (gluing ğ’° f g).Î¹ i
  intro i j
  rw [â† gluedLiftPullbackMap_fst_assoc]
  have : _ = pullback.fst â‰« _ := (gluing ğ’° f g).glue_condition i j
  rw [â† this, gluing_t, gluing_f]
  simp_rw [â† Category.assoc]
  congr 1
  apply pullback.hom_ext <;> simp_rw [Category.assoc]
  Â· rw [t_fst_fst, gluedLiftPullbackMap_snd]
    congr 1
    rw [â† Iso.inv_comp_eq, pullbackSymmetry_inv_comp_snd]
    erw [pullback.lift_fst]
    rw [Category.comp_id]
  Â· rw [t_fst_snd, gluedLiftPullbackMap_fst_assoc]
    erw [pullback.lift_snd, pullback.lift_snd]
    rw [pullbackSymmetry_hom_comp_snd_assoc, pullbackSymmetry_hom_comp_snd_assoc]
    exact pullback.condition_assoc _
#align algebraic_geometry.Scheme.pullback.glued_lift AlgebraicGeometry.Scheme.Pullback.gluedLift

theorem gluedLift_p1 : gluedLift ğ’° f g s â‰« p1 ğ’° f g = s.fst := by
  rw [â† cancel_epi (ğ’°.pullbackCover s.fst).fromGlued]
  apply Multicoequalizer.hom_ext
  intro b
  erw [Multicoequalizer.Ï€_desc_assoc, Multicoequalizer.Ï€_desc_assoc]
  delta gluedLift
  simp_rw [â† Category.assoc]
  rw [(ğ’°.pullbackCover s.fst).Î¹_glueMorphisms]
  simp_rw [Category.assoc]
  -- Porting note: `Category.comp_id` is no longer necessary, don't know where `ğŸ™ _` has gone
  erw [Multicoequalizer.Ï€_desc, pullback.lift_fst_assoc, pullback.condition]
  rw [pullbackSymmetry_hom_comp_snd_assoc]
  rfl
#align algebraic_geometry.Scheme.pullback.glued_lift_p1 AlgebraicGeometry.Scheme.Pullback.gluedLift_p1

theorem gluedLift_p2 : gluedLift ğ’° f g s â‰« p2 ğ’° f g = s.snd := by
  rw [â† cancel_epi (ğ’°.pullbackCover s.fst).fromGlued]
  apply Multicoequalizer.hom_ext
  intro b
  erw [Multicoequalizer.Ï€_desc_assoc, Multicoequalizer.Ï€_desc_assoc]
  delta gluedLift
  simp_rw [â† Category.assoc]
  rw [(ğ’°.pullbackCover s.fst).Î¹_glueMorphisms]
  simp_rw [Category.assoc]
  erw [Multicoequalizer.Ï€_desc, pullback.lift_snd]
  rw [pullbackSymmetry_hom_comp_snd_assoc]
  rfl
#align algebraic_geometry.Scheme.pullback.glued_lift_p2 AlgebraicGeometry.Scheme.Pullback.gluedLift_p2

/-- (Implementation)
The canonical map `(W Ã—[X] Uáµ¢) Ã—[W] (Uâ±¼ Ã—[Z] Y) âŸ¶ (Uâ±¼ Ã—[Z] Y) Ã—[X] Uáµ¢ = V j i` where `W` is
the glued fibred product.

This is used in `lift_comp_Î¹`. -/
def pullbackFstÎ¹ToV (i j : ğ’°.J) :
    pullback (pullback.fst : pullback (p1 ğ’° f g) (ğ’°.map i) âŸ¶ _) ((gluing ğ’° f g).Î¹ j) âŸ¶
      v ğ’° f g j i :=
  (pullbackSymmetry _ _ â‰ªâ‰« pullbackRightPullbackFstIso (p1 ğ’° f g) (ğ’°.map i) _).hom â‰«
    (pullback.congrHom (Multicoequalizer.Ï€_desc _ _ _ _ _) rfl).hom
#align algebraic_geometry.Scheme.pullback.pullback_fst_Î¹_to_V AlgebraicGeometry.Scheme.Pullback.pullbackFstÎ¹ToV

@[simp, reassoc]
theorem pullbackFstÎ¹ToV_fst (i j : ğ’°.J) :
    pullbackFstÎ¹ToV ğ’° f g i j â‰« pullback.fst = pullback.snd := by
  delta pullbackFstÎ¹ToV
  simp only [Iso.trans_hom, pullback.congrHom_hom, Category.assoc, pullback.lift_fst,
    Category.comp_id]
  -- Porting note: `pullbackRightPullbackFstIso_hom_fst` is not used in `simp` even though
  -- instructed, forcing `pullbackSymmetry_hom_comp_fst` to be manually `rw`ed as well.
  erw [pullbackRightPullbackFstIso_hom_fst]
  rw [pullbackSymmetry_hom_comp_fst]
#align algebraic_geometry.Scheme.pullback.pullback_fst_Î¹_to_V_fst AlgebraicGeometry.Scheme.Pullback.pullbackFstÎ¹ToV_fst

@[simp, reassoc]
theorem pullbackFstÎ¹ToV_snd (i j : ğ’°.J) :
    pullbackFstÎ¹ToV ğ’° f g i j â‰« pullback.snd = pullback.fst â‰« pullback.snd := by
  delta pullbackFstÎ¹ToV
  simp only [Iso.trans_hom, pullback.congrHom_hom, Category.assoc, pullback.lift_snd,
    Category.comp_id]
  -- Porting note: `pullbackRightPullbackFstIso_hom_snd` is not used in `simp` even though
  -- instructed, forcing `pullbackSymmetry_hom_comp_snd_assoc` to be manually `rw`ed as well.
  erw [pullbackRightPullbackFstIso_hom_snd]
  rw [pullbackSymmetry_hom_comp_snd_assoc]
#align algebraic_geometry.Scheme.pullback.pullback_fst_Î¹_to_V_snd AlgebraicGeometry.Scheme.Pullback.pullbackFstÎ¹ToV_snd

/-- We show that the map `W Ã—[X] Uáµ¢ âŸ¶ Uáµ¢ Ã—[Z] Y âŸ¶ W` is the first projection, where the
first map is given by the lift of `W Ã—[X] Uáµ¢ âŸ¶ Uáµ¢` and `W Ã—[X] Uáµ¢ âŸ¶ W âŸ¶ Y`.

It suffices to show that the two map agrees when restricted onto `Uâ±¼ Ã—[Z] Y`. In this case,
both maps factor through `V j i` via `pullback_fst_Î¹_to_V` -/
theorem lift_comp_Î¹ (i : ğ’°.J) :
    pullback.lift pullback.snd (pullback.fst â‰« p2 ğ’° f g)
          (by rw [â† pullback.condition_assoc, Category.assoc, p_comm]) â‰«
        (gluing ğ’° f g).Î¹ i =
      (pullback.fst : pullback (p1 ğ’° f g) (ğ’°.map i) âŸ¶ _) := by
  apply ((gluing ğ’° f g).openCover.pullbackCover pullback.fst).hom_ext
  intro j
  dsimp only [OpenCover.pullbackCover]
  trans pullbackFstÎ¹ToV ğ’° f g i j â‰« fV ğ’° f g j i â‰« (gluing ğ’° f g).Î¹ _
  Â· rw [â† show _ = fV ğ’° f g j i â‰« _ from (gluing ğ’° f g).glue_condition j i]
    simp_rw [â† Category.assoc]
    congr 1
    rw [gluing_f, gluing_t]
    apply pullback.hom_ext <;> simp_rw [Category.assoc]
    -- Porting note: in the following two bullet points, `rfl` was not necessary
    Â· rw [t_fst_fst, pullback.lift_fst, pullbackFstÎ¹ToV_snd]; rfl
    Â· rw [t_fst_snd, pullback.lift_snd, pullbackFstÎ¹ToV_fst_assoc, pullback.condition_assoc]
      erw [Multicoequalizer.Ï€_desc]
      rfl
  Â· rw [pullback.condition, â† Category.assoc]
    congr 1
    apply pullback.hom_ext
    -- Porting note: in the following two bullet points, `rfl` was not necessary
    Â· simp only [pullbackFstÎ¹ToV_fst]; rfl
    Â· simp only [pullbackFstÎ¹ToV_fst]; rfl
#align algebraic_geometry.Scheme.pullback.lift_comp_Î¹ AlgebraicGeometry.Scheme.Pullback.lift_comp_Î¹

/-- The canonical isomorphism between `W Ã—[X] Uáµ¢` and `Uáµ¢ Ã—[X] Y`. That is, the preimage of `Uáµ¢` in
`W` along `p1` is indeed `Uáµ¢ Ã—[X] Y`. -/
def pullbackP1Iso (i : ğ’°.J) : pullback (p1 ğ’° f g) (ğ’°.map i) â‰… pullback (ğ’°.map i â‰« f) g := by
  fconstructor
  exact
    pullback.lift pullback.snd (pullback.fst â‰« p2 ğ’° f g)
      (by rw [â† pullback.condition_assoc, Category.assoc, p_comm])
  refine' pullback.lift ((gluing ğ’° f g).Î¹ i) pullback.fst (by erw [Multicoequalizer.Ï€_desc])
  Â· apply pullback.hom_ext
    Â· simpa using lift_comp_Î¹ ğ’° f g i
    Â· simp only [Category.assoc, pullback.lift_snd, pullback.lift_fst, Category.id_comp]
  Â· apply pullback.hom_ext
    Â· simp only [Category.assoc, pullback.lift_fst, pullback.lift_snd, Category.id_comp]
    Â· simp only [Category.assoc, pullback.lift_snd, pullback.lift_fst_assoc, Category.id_comp]
      erw [Multicoequalizer.Ï€_desc]
#align algebraic_geometry.Scheme.pullback.pullback_p1_iso AlgebraicGeometry.Scheme.Pullback.pullbackP1Iso

@[simp, reassoc]
theorem pullbackP1Iso_hom_fst (i : ğ’°.J) :
    (pullbackP1Iso ğ’° f g i).hom â‰« pullback.fst = pullback.snd := by
  delta pullbackP1Iso
  simp only [pullback.lift_fst]
#align algebraic_geometry.Scheme.pullback.pullback_p1_iso_hom_fst AlgebraicGeometry.Scheme.Pullback.pullbackP1Iso_hom_fst

@[simp, reassoc]
theorem pullbackP1Iso_hom_snd (i : ğ’°.J) :
    (pullbackP1Iso ğ’° f g i).hom â‰« pullback.snd = pullback.fst â‰« p2 ğ’° f g := by
  delta pullbackP1Iso; simp only [pullback.lift_snd]
#align algebraic_geometry.Scheme.pullback.pullback_p1_iso_hom_snd AlgebraicGeometry.Scheme.Pullback.pullbackP1Iso_hom_snd

@[simp, reassoc]
theorem pullbackP1Iso_inv_fst (i : ğ’°.J) :
    (pullbackP1Iso ğ’° f g i).inv â‰« pullback.fst = (gluing ğ’° f g).Î¹ i := by
  delta pullbackP1Iso; simp only [pullback.lift_fst]
#align algebraic_geometry.Scheme.pullback.pullback_p1_iso_inv_fst AlgebraicGeometry.Scheme.Pullback.pullbackP1Iso_inv_fst

@[simp, reassoc]
theorem pullbackP1Iso_inv_snd (i : ğ’°.J) :
    (pullbackP1Iso ğ’° f g i).inv â‰« pullback.snd = pullback.fst := by
  delta pullbackP1Iso; simp only [pullback.lift_snd]
#align algebraic_geometry.Scheme.pullback.pullback_p1_iso_inv_snd AlgebraicGeometry.Scheme.Pullback.pullbackP1Iso_inv_snd

@[simp, reassoc]
theorem pullbackP1Iso_hom_Î¹ (i : ğ’°.J) :
    (pullbackP1Iso ğ’° f g i).hom â‰« (gluing ğ’° f g).Î¹ i = pullback.fst := by
  rw [â† pullbackP1Iso_inv_fst, Iso.hom_inv_id_assoc]
#align algebraic_geometry.Scheme.pullback.pullback_p1_iso_hom_Î¹ AlgebraicGeometry.Scheme.Pullback.pullbackP1Iso_hom_Î¹

/-- The glued scheme (`(gluing ğ’° f g).glued`) is indeed the pullback of `f` and `g`. -/
def gluedIsLimit : IsLimit (PullbackCone.mk _ _ (p_comm ğ’° f g)) := by
  apply PullbackCone.isLimitAux'
  intro s
  refine' âŸ¨gluedLift ğ’° f g s, gluedLift_p1 ğ’° f g s, gluedLift_p2 ğ’° f g s, _âŸ©
  intro m hâ‚ hâ‚‚
  change m â‰« p1 ğ’° f g = _ at hâ‚
  change m â‰« p2 ğ’° f g = _ at hâ‚‚
  apply (ğ’°.pullbackCover s.fst).hom_ext
  intro i
  rw [OpenCover.pullbackCover_map]
  have := pullbackRightPullbackFstIso (p1 ğ’° f g) (ğ’°.map i) m â‰ªâ‰« pullback.congrHom hâ‚ rfl
  erw [(ğ’°.pullbackCover s.fst).Î¹_glueMorphisms]
  rw [â†
    cancel_epi
      (pullbackRightPullbackFstIso (p1 ğ’° f g) (ğ’°.map i) m â‰ªâ‰« pullback.congrHom hâ‚ rfl).hom,
    Iso.trans_hom, Category.assoc, pullback.congrHom_hom, pullback.lift_fst_assoc,
    Category.comp_id, pullbackRightPullbackFstIso_hom_fst_assoc, pullback.condition]
  trans pullback.snd â‰« (pullbackP1Iso ğ’° f g _).hom â‰« (gluing ğ’° f g).Î¹ _
  Â· congr 1; rw [â† pullbackP1Iso_hom_Î¹]
  simp_rw [â† Category.assoc]
  congr 1
  apply pullback.hom_ext
  Â· simp only [Category.comp_id, pullbackRightPullbackFstIso_hom_snd, Category.assoc,
      pullbackP1Iso_hom_fst, pullback.lift_snd, pullback.lift_fst, pullbackSymmetry_hom_comp_fst]
  Â· simp only [Category.comp_id, pullbackRightPullbackFstIso_hom_fst_assoc,
      pullbackP1Iso_hom_snd, Category.assoc, pullback.lift_fst_assoc,
      pullbackSymmetry_hom_comp_snd_assoc, pullback.lift_snd]
    rw [â† pullback.condition_assoc, hâ‚‚]
#align algebraic_geometry.Scheme.pullback.glued_is_limit AlgebraicGeometry.Scheme.Pullback.gluedIsLimit

theorem hasPullback_of_cover : HasPullback f g :=
  âŸ¨âŸ¨âŸ¨_, gluedIsLimit ğ’° f gâŸ©âŸ©âŸ©
#align algebraic_geometry.Scheme.pullback.has_pullback_of_cover AlgebraicGeometry.Scheme.Pullback.hasPullback_of_cover

instance affine_hasPullback {A B C : CommRingCat}
    (f : Spec.obj (Opposite.op A) âŸ¶ Spec.obj (Opposite.op C))
    (g : Spec.obj (Opposite.op B) âŸ¶ Spec.obj (Opposite.op C)) : HasPullback f g := by
  rw [â† Spec.image_preimage f, â† Spec.image_preimage g]
  exact
    âŸ¨âŸ¨âŸ¨_, isLimitOfHasPullbackOfPreservesLimit Spec (Spec.preimage f) (Spec.preimage g)âŸ©âŸ©âŸ©
#align algebraic_geometry.Scheme.pullback.affine_has_pullback AlgebraicGeometry.Scheme.Pullback.affine_hasPullback

theorem affine_affine_hasPullback {B C : CommRingCat} {X : Scheme}
    (f : X âŸ¶ Spec.obj (Opposite.op C)) (g : Spec.obj (Opposite.op B) âŸ¶ Spec.obj (Opposite.op C)) :
    HasPullback f g :=
  hasPullback_of_cover X.affineCover f g
#align algebraic_geometry.Scheme.pullback.affine_affine_has_pullback AlgebraicGeometry.Scheme.Pullback.affine_affine_hasPullback

instance base_affine_hasPullback {C : CommRingCat} {X Y : Scheme} (f : X âŸ¶ Spec.obj (Opposite.op C))
    (g : Y âŸ¶ Spec.obj (Opposite.op C)) : HasPullback f g :=
  @hasPullback_symmetry _ _ _ _ _ _ _
    (@hasPullback_of_cover _ _ _ Y.affineCover g f fun _ =>
      @hasPullback_symmetry _ _ _ _ _ _ _ <| affine_affine_hasPullback _ _)
#align algebraic_geometry.Scheme.pullback.base_affine_has_pullback AlgebraicGeometry.Scheme.Pullback.base_affine_hasPullback

instance left_affine_comp_pullback_hasPullback {X Y Z : Scheme} (f : X âŸ¶ Z) (g : Y âŸ¶ Z)
    (i : Z.affineCover.J) : HasPullback ((Z.affineCover.pullbackCover f).map i â‰« f) g := by
  let Xáµ¢ := pullback f (Z.affineCover.map i)
  let Yáµ¢ := pullback g (Z.affineCover.map i)
  let W := pullback (pullback.snd : Yáµ¢ âŸ¶ _) (pullback.snd : Xáµ¢ âŸ¶ _)
  have :=
    bigSquareIsPullback (pullback.fst : W âŸ¶ _) (pullback.fst : Yáµ¢ âŸ¶ _) (pullback.snd : Xáµ¢ âŸ¶ _)
      (Z.affineCover.map i) pullback.snd pullback.snd g pullback.condition.symm
      pullback.condition.symm (PullbackCone.isLimitOfFlip <| pullbackIsPullback _ _)
      (PullbackCone.isLimitOfFlip <| pullbackIsPullback _ _)
  have : HasPullback (pullback.snd â‰« Z.affineCover.map i : Xáµ¢ âŸ¶ _) g := âŸ¨âŸ¨âŸ¨_, thisâŸ©âŸ©âŸ©
  rw [â† pullback.condition] at this
  exact this
#align algebraic_geometry.Scheme.pullback.left_affine_comp_pullback_HasPullback AlgebraicGeometry.Scheme.Pullback.left_affine_comp_pullback_hasPullback

instance {X Y Z : Scheme} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) : HasPullback f g :=
  hasPullback_of_cover (Z.affineCover.pullbackCover f) f g

instance : HasPullbacks Scheme :=
  hasPullbacks_of_hasLimit_cospan _

instance isAffine_of_isAffine_isAffine_isAffine {X Y Z : Scheme}
    (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [IsAffine X] [IsAffine Y] [IsAffine Z] :
    IsAffine (pullback f g) :=
  isAffineOfIso
    (pullback.map f g (Spec.map (Î“.map f.op).op) (Spec.map (Î“.map g.op).op)
        (Î“Spec.adjunction.unit.app X) (Î“Spec.adjunction.unit.app Y) (Î“Spec.adjunction.unit.app Z)
        (Î“Spec.adjunction.unit.naturality f) (Î“Spec.adjunction.unit.naturality g) â‰«
      (PreservesPullback.iso Spec _ _).inv)

/-- Given an open cover `{ Xáµ¢ }` of `X`, then `X Ã—[Z] Y` is covered by `Xáµ¢ Ã—[Z] Y`. -/
@[simps! J obj map]
def openCoverOfLeft (ğ’° : OpenCover X) (f : X âŸ¶ Z) (g : Y âŸ¶ Z) : OpenCover (pullback f g) := by
  fapply
    ((gluing ğ’° f g).openCover.pushforwardIso
          (limit.isoLimitCone âŸ¨_, gluedIsLimit ğ’° f gâŸ©).inv).copy
      ğ’°.J (fun i => pullback (ğ’°.map i â‰« f) g)
      (fun i => pullback.map _ _ _ _ (ğ’°.map i) (ğŸ™ _) (ğŸ™ _) (Category.comp_id _) (by simp))
      (Equiv.refl ğ’°.J) fun _ => Iso.refl _
  rintro (i : ğ’°.J)
  change pullback.map _ _ _ _ _ _ _ _ _ = ğŸ™ _ â‰« (gluing ğ’° f g).Î¹ i â‰« _
  refine' Eq.trans _ (Category.id_comp _).symm
  apply pullback.hom_ext
  all_goals
    dsimp
    simp only [limit.isoLimitCone_inv_Ï€, PullbackCone.mk_Ï€_app_left, Category.comp_id,
      PullbackCone.mk_Ï€_app_right, Category.assoc, pullback.lift_fst, pullback.lift_snd]
    symm
    exact Multicoequalizer.Ï€_desc _ _ _ _ _
#align algebraic_geometry.Scheme.pullback.open_cover_of_left AlgebraicGeometry.Scheme.Pullback.openCoverOfLeft

/-- Given an open cover `{ Yáµ¢ }` of `Y`, then `X Ã—[Z] Y` is covered by `X Ã—[Z] Yáµ¢`. -/
@[simps! J obj map]
def openCoverOfRight (ğ’° : OpenCover Y) (f : X âŸ¶ Z) (g : Y âŸ¶ Z) : OpenCover (pullback f g) := by
  fapply
    ((openCoverOfLeft ğ’° g f).pushforwardIso (pullbackSymmetry _ _).hom).copy ğ’°.J
      (fun i => pullback f (ğ’°.map i â‰« g))
      (fun i => pullback.map _ _ _ _ (ğŸ™ _) (ğ’°.map i) (ğŸ™ _) (by simp) (Category.comp_id _))
      (Equiv.refl _) fun i => pullbackSymmetry _ _
  intro i
  dsimp [OpenCover.bind]
  apply pullback.hom_ext <;> simp
#align algebraic_geometry.Scheme.pullback.open_cover_of_right AlgebraicGeometry.Scheme.Pullback.openCoverOfRight

/-- Given an open cover `{ Xáµ¢ }` of `X` and an open cover `{ Yâ±¼ }` of `Y`, then
`X Ã—[Z] Y` is covered by `Xáµ¢ Ã—[Z] Yâ±¼`. -/
@[simps! J obj map]
def openCoverOfLeftRight (ğ’°X : X.OpenCover) (ğ’°Y : Y.OpenCover) (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
    (pullback f g).OpenCover := by
  fapply
    ((openCoverOfLeft ğ’°X f g).bind fun x => openCoverOfRight ğ’°Y (ğ’°X.map x â‰« f) g).copy
      (ğ’°X.J Ã— ğ’°Y.J) (fun ij => pullback (ğ’°X.map ij.1 â‰« f) (ğ’°Y.map ij.2 â‰« g))
      (fun ij =>
        pullback.map _ _ _ _ (ğ’°X.map ij.1) (ğ’°Y.map ij.2) (ğŸ™ _) (Category.comp_id _)
          (Category.comp_id _))
      (Equiv.sigmaEquivProd _ _).symm fun _ => Iso.refl _
  rintro âŸ¨i, jâŸ©
  apply pullback.hom_ext <;> simp
#align algebraic_geometry.Scheme.pullback.open_cover_of_left_right AlgebraicGeometry.Scheme.Pullback.openCoverOfLeftRight

/-- (Implementation). Use `openCoverOfBase` instead. -/
@[simps! map]
def openCoverOfBase' (ğ’° : OpenCover Z) (f : X âŸ¶ Z) (g : Y âŸ¶ Z) : OpenCover (pullback f g) := by
  apply (openCoverOfLeft (ğ’°.pullbackCover f) f g).bind
  intro i
  let Xáµ¢ := pullback f (ğ’°.map i)
  let Yáµ¢ := pullback g (ğ’°.map i)
  let W := pullback (pullback.snd : Yáµ¢ âŸ¶ _) (pullback.snd : Xáµ¢ âŸ¶ _)
  have :=
    bigSquareIsPullback (pullback.fst : W âŸ¶ _) (pullback.fst : Yáµ¢ âŸ¶ _) (pullback.snd : Xáµ¢ âŸ¶ _)
      (ğ’°.map i) pullback.snd pullback.snd g pullback.condition.symm pullback.condition.symm
      (PullbackCone.isLimitOfFlip <| pullbackIsPullback _ _)
      (PullbackCone.isLimitOfFlip <| pullbackIsPullback _ _)
  refine'
    @openCoverOfIsIso
      (f := (pullbackSymmetry _ _).hom â‰«
        (limit.isoLimitCone âŸ¨_, thisâŸ©).inv â‰« pullback.map _ _ _ _ (ğŸ™ _) (ğŸ™ _) (ğŸ™ _) _ _) ?_
  Â· simp only [Category.comp_id, Category.id_comp, â† pullback.condition]
    -- Porting note: `simpa` failed, but this is indeed `rfl`
    rfl
  Â· simp only [Category.comp_id, Category.id_comp]
  -- Porting note: this `IsIso` instance was `inferInstance`
  Â· apply IsIso.comp_isIso
#align algebraic_geometry.Scheme.pullback.open_cover_of_base' AlgebraicGeometry.Scheme.Pullback.openCoverOfBase'

/-- Given an open cover `{ Záµ¢ }` of `Z`, then `X Ã—[Z] Y` is covered by `Xáµ¢ Ã—[Záµ¢] Yáµ¢`, where
  `Xáµ¢ = X Ã—[Z] Záµ¢` and `Yáµ¢ = Y Ã—[Z] Záµ¢` is the preimage of `Záµ¢` in `X` and `Y`. -/
@[simps! J obj map]
def openCoverOfBase (ğ’° : OpenCover Z) (f : X âŸ¶ Z) (g : Y âŸ¶ Z) : OpenCover (pullback f g) := by
  apply
    (openCoverOfBase'.{u, u} ğ’° f g).copy ğ’°.J
      (fun i =>
        pullback (pullback.snd : pullback f (ğ’°.map i) âŸ¶ _)
          (pullback.snd : pullback g (ğ’°.map i) âŸ¶ _))
      (fun i =>
        pullback.map _ _ _ _ pullback.fst pullback.fst (ğ’°.map i) pullback.condition.symm
          pullback.condition.symm)
      ((Equiv.prodPUnit ğ’°.J).symm.trans (Equiv.sigmaEquivProd ğ’°.J PUnit).symm) fun _ => Iso.refl _
  intro i
  -- Porting note: deviated from original proof a bit so that it won't timeout.
  rw [Iso.refl_hom, Category.id_comp, openCoverOfBase'_map]
  apply pullback.hom_ext <;> dsimp <;>
  Â· simp only [limit.lift_Ï€, PullbackCone.mk_pt, PullbackCone.mk_Ï€_app, Category.assoc,
      limit.lift_Ï€_assoc, cospan_left, Category.comp_id, limit.isoLimitCone_inv_Ï€,
      limit.isoLimitCone_inv_Ï€_assoc, pullbackSymmetry_hom_comp_fst_assoc,
      pullbackSymmetry_hom_comp_snd_assoc]
    rfl
#align algebraic_geometry.Scheme.pullback.open_cover_of_base AlgebraicGeometry.Scheme.Pullback.openCoverOfBase

end Pullback

end AlgebraicGeometry.Scheme

namespace AlgebraicGeometry

instance Scheme.pullback_map_isOpenImmersion {X Y S X' Y' S' : Scheme}
    (f : X âŸ¶ S) (g : Y âŸ¶ S) (f' : X' âŸ¶ S') (g' : Y' âŸ¶ S')
    (iâ‚ : X âŸ¶ X') (iâ‚‚ : Y âŸ¶ Y') (iâ‚ƒ : S âŸ¶ S') (eâ‚ : f â‰« iâ‚ƒ = iâ‚ â‰« f') (eâ‚‚ : g â‰« iâ‚ƒ = iâ‚‚ â‰« g')
    [IsOpenImmersion iâ‚] [IsOpenImmersion iâ‚‚] [Mono iâ‚ƒ] :
    IsOpenImmersion (pullback.map f g f' g' iâ‚ iâ‚‚ iâ‚ƒ eâ‚ eâ‚‚) := by
  rw [pullback_map_eq_pullbackFstFstIso_inv]
  -- Porting note: was automatic
  exact PresheafedSpace.IsOpenImmersion.comp _ (hg := PresheafedSpace.IsOpenImmersion.comp _ _)

end AlgebraicGeometry
