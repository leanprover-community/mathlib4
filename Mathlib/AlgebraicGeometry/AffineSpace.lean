/-
Copyright (c) 2024 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Algebra.MvPolynomial.Monad
import Mathlib.AlgebraicGeometry.Morphisms.Finite
import Mathlib.AlgebraicGeometry.Morphisms.FinitePresentation
import Mathlib.RingTheory.Spectrum.Prime.Polynomial
import Mathlib.AlgebraicGeometry.PullbackCarrier

/-!
# Affine space

## Main definitions

- `AlgebraicGeometry.AffineSpace`: `ğ”¸(n; S)` is the affine `n`-space over `S`.
- `AlgebraicGeometry.AffineSpace.coord`: The standard coordinate functions on the affine space.
- `AlgebraicGeometry.AffineSpace.homOfVector`:
  The morphism `X âŸ¶ ğ”¸(n; S)` given by a `X âŸ¶ S` and a choice of `n`-coordinate functions.
- `AlgebraicGeometry.AffineSpace.homOverEquiv`:
  `S`-morphisms into `Spec ğ”¸(n; S)` are equivalent to the choice of `n` global sections.
- `AlgebraicGeometry.AffineSpace.SpecIso`: `ğ”¸(n; Spec R) â‰… Spec R[n]`

-/

open CategoryTheory Limits MvPolynomial

noncomputable section

namespace AlgebraicGeometry

universe v u

variable (n : Type v) (S : Scheme.{max u v})

local notation3 "â„¤[" n "]" => CommRingCat.of (MvPolynomial n (ULift â„¤))
local notation3 "â„¤[" n "].{" u "," v "}" => CommRingCat.of (MvPolynomial n (ULift.{max u v} â„¤))

/-- `ğ”¸(n; S)` is the affine `n`-space over `S`.
Note that `n` is an arbitrary index type (e.g. `Fin m`). -/
def AffineSpace (n : Type v) (S : Scheme.{max u v}) : Scheme.{max u v} :=
  pullback (terminal.from S) (terminal.from (Spec â„¤[n].{u, v}))

namespace AffineSpace

/-- `ğ”¸(n; S)` is the affine `n`-space over `S`. -/
scoped[AlgebraicGeometry] notation "ğ”¸("n"; "S")" => AffineSpace n S

variable {n} in
lemma of_mvPolynomial_int_ext {R} {f g : â„¤[n] âŸ¶ R} (h : âˆ€ i, f (.X i) = g (.X i)) : f = g := by
  suffices f.hom.comp (MvPolynomial.mapEquiv _ ULift.ringEquiv.symm).toRingHom =
      g.hom.comp (MvPolynomial.mapEquiv _ ULift.ringEquiv.symm).toRingHom by
    ext x
    Â· obtain âŸ¨xâŸ© := x
      simpa [-map_intCast, -eq_intCast] using DFunLike.congr_fun this (C x)
    Â· simpa [-map_intCast, -eq_intCast] using DFunLike.congr_fun this (X x)
  ext1
  Â· exact RingHom.ext_int _ _
  Â· simpa using h _


@[simps -isSimp]
instance over : ğ”¸(n; S).CanonicallyOver S where
  hom := pullback.fst _ _

/-- The map from the affine `n`-space over `S` to the integral model `Spec â„¤[n]`. -/
def toSpecMvPoly : ğ”¸(n; S) âŸ¶ Spec â„¤[n].{u, v} := pullback.snd _ _

variable {X : Scheme.{max u v}}

/--
Morphisms into `Spec â„¤[n]` are equivalent the choice of `n` global sections.
Use `homOverEquiv` instead.
-/
@[simps]
def toSpecMvPolyIntEquiv : (X âŸ¶ Spec â„¤[n]) â‰ƒ (n â†’ Î“(X, âŠ¤)) where
  toFun f i := f.appTop ((Scheme.Î“SpecIso â„¤[n]).inv (.X i))
  invFun v := X.toSpecÎ“ â‰« Spec.map
    (CommRingCat.ofHom (MvPolynomial.evalâ‚‚Hom ((algebraMap â„¤ _).comp ULift.ringEquiv.toRingHom) v))
  left_inv f := by
    apply (Î“Spec.adjunction.homEquiv _ _).symm.injective
    apply Quiver.Hom.unop_inj
    rw [Adjunction.homEquiv_symm_apply, Adjunction.homEquiv_symm_apply]
    dsimp
    simp only [Scheme.toSpecÎ“_appTop, Scheme.Î“SpecIso_naturality, Iso.inv_hom_id_assoc]
    apply of_mvPolynomial_int_ext
    intro i
    rw [ConcreteCategory.hom_ofHom, coe_evalâ‚‚Hom, evalâ‚‚_X]
    rfl
  right_inv v := by ext; simp

lemma toSpecMvPolyIntEquiv_comp {X Y : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Spec â„¤[n]) (i) :
    toSpecMvPolyIntEquiv n (f â‰« g) i = f.appTop (toSpecMvPolyIntEquiv n g i) := rfl

variable {n} in
/-- The standard coordinates of `ğ”¸(n; S)`. -/
def coord (i : n) : Î“(ğ”¸(n; S), âŠ¤) := toSpecMvPolyIntEquiv _ (toSpecMvPoly n S) i

section homOfVector

variable {n S}

/-- The morphism `X âŸ¶ ğ”¸(n; S)` given by a `X âŸ¶ S` and a choice of `n`-coordinate functions. -/
def homOfVector (f : X âŸ¶ S) (v : n â†’ Î“(X, âŠ¤)) : X âŸ¶ ğ”¸(n; S) :=
  pullback.lift f ((toSpecMvPolyIntEquiv n).symm v) (by simp)

variable (f : X âŸ¶ S) (v : n â†’ Î“(X, âŠ¤))

@[reassoc (attr := simp)]
lemma homOfVector_over : homOfVector f v â‰« ğ”¸(n; S) â†˜ S = f :=
  pullback.lift_fst _ _ _

@[reassoc]
lemma homOfVector_toSpecMvPoly :
    homOfVector f v â‰« toSpecMvPoly n S = (toSpecMvPolyIntEquiv n).symm v :=
  pullback.lift_snd _ _ _

@[simp]
lemma homOfVector_appTop_coord (i) :
    (homOfVector f v).appTop (coord S i) = v i := by
  rw [coord, â† toSpecMvPolyIntEquiv_comp, homOfVector_toSpecMvPoly,
    Equiv.apply_symm_apply]

@[ext 1100]
lemma hom_ext {f g : X âŸ¶ ğ”¸(n; S)}
    (hâ‚ : f â‰« ğ”¸(n; S) â†˜ S = g â‰« ğ”¸(n; S) â†˜ S)
    (hâ‚‚ : âˆ€ i, f.appTop (coord S i) = g.appTop (coord S i)) : f = g := by
  apply pullback.hom_ext hâ‚
  change f â‰« toSpecMvPoly _ _ = g â‰« toSpecMvPoly _ _
  apply (toSpecMvPolyIntEquiv n).injective
  ext i
  rw [toSpecMvPolyIntEquiv_comp, toSpecMvPolyIntEquiv_comp]
  exact hâ‚‚ i

@[reassoc]
lemma comp_homOfVector {X Y : Scheme} (v : n â†’ Î“(Y, âŠ¤)) (f : X âŸ¶ Y) (g : Y âŸ¶ S) :
    f â‰« homOfVector g v = homOfVector (f â‰« g) (f.appTop âˆ˜ v) := by
  ext1 <;> simp

end homOfVector

variable [X.Over S]

variable {n}

instance (v : n â†’ Î“(X, âŠ¤)) : (homOfVector (X â†˜ S) v).IsOver S where

/-- `S`-morphisms into `Spec ğ”¸(n; S)` are equivalent to the choice of `n` global sections. -/
@[simps]
def homOverEquiv : { f : X âŸ¶ ğ”¸(n; S) // f.IsOver S } â‰ƒ (n â†’ Î“(X, âŠ¤)) where
  toFun f i := f.1.appTop (coord S i)
  invFun v := âŸ¨homOfVector (X â†˜ S) v, inferInstanceâŸ©
  left_inv f := by
    ext : 2
    Â· simp [f.2.1]
    Â· rw [homOfVector_appTop_coord]
  right_inv v := by ext i; simp [-TopologicalSpace.Opens.map_top, homOfVector_appTop_coord]

variable (n) in
/--
The affine space over an affine base is isomorphic to the spectrum of the polynomial ring.
Also see `AffineSpace.SpecIso`.
-/
@[simps -isSimp hom inv]
def isoOfIsAffine [IsAffine S] :
    ğ”¸(n; S) â‰… Spec <| .of <| MvPolynomial n Î“(S, âŠ¤) where
      hom := ğ”¸(n; S).toSpecÎ“ â‰« Spec.map (CommRingCat.ofHom
        (evalâ‚‚Hom ((ğ”¸(n; S) â†˜ S).appTop).hom (coord S)))
      inv := homOfVector (Spec.map (CommRingCat.ofHom C) â‰« S.isoSpec.inv)
        ((Scheme.Î“SpecIso (.of (MvPolynomial n Î“(S, âŠ¤)))).inv âˆ˜ MvPolynomial.X)
      hom_inv_id := by
        ext1
        Â· simp only [Category.assoc, homOfVector_over, Category.id_comp]
          rw [â† Spec.map_comp_assoc, â† CommRingCat.ofHom_comp, evalâ‚‚Hom_comp_C,
            CommRingCat.ofHom_hom, â† Scheme.toSpecÎ“_naturality_assoc]
          simp [Scheme.isoSpec]
        Â· simp
      inv_hom_id := by
        apply ext_of_isAffine
        simp only [Scheme.Hom.comp_base, TopologicalSpace.Opens.map_comp_obj,
          TopologicalSpace.Opens.map_top, Scheme.Hom.comp_app, Scheme.toSpecÎ“_appTop,
          Scheme.Î“SpecIso_naturality, Category.assoc, Scheme.Hom.id_app, â† Iso.eq_inv_comp,
          Category.comp_id]
        ext : 1
        apply ringHom_ext'
        Â· change _ = (CommRingCat.ofHom C â‰« _).hom
          rw [CommRingCat.hom_comp, RingHom.comp_assoc, CommRingCat.hom_ofHom, evalâ‚‚Hom_comp_C,
            â† CommRingCat.hom_comp, â† CommRingCat.hom_ext_iff,
            â† cancel_mono (Scheme.Î“SpecIso _).hom]
          rw [â† Scheme.Hom.comp_appTop, homOfVector_over, Scheme.Hom.comp_appTop]
          simp only [Category.assoc, Scheme.Î“SpecIso_naturality, CommRingCat.of_carrier,
            â† Scheme.toSpecÎ“_appTop]
          rw [â† Scheme.Hom.comp_appTop_assoc, Scheme.isoSpec, asIso_inv, IsIso.hom_inv_id]
          simp
        Â· intro i
          rw [CommRingCat.comp_apply, ConcreteCategory.hom_ofHom, coe_evalâ‚‚Hom]
          simp only [evalâ‚‚_X]
          exact homOfVector_appTop_coord _ _ _

@[simp]
lemma isoOfIsAffine_hom_appTop [IsAffine S] :
    (isoOfIsAffine n S).hom.appTop =
      (Scheme.Î“SpecIso _).hom â‰« CommRingCat.ofHom
        (evalâ‚‚Hom ((ğ”¸(n; S) â†˜ S).appTop).hom (coord S)) := by
  simp [isoOfIsAffine_hom]

@[simp]
lemma isoOfIsAffine_inv_appTop_coord [IsAffine S] (i) :
    (isoOfIsAffine n S).inv.appTop (coord _ i) = (Scheme.Î“SpecIso (.of _)).inv (.X i) :=
  homOfVector_appTop_coord _ _ _

@[reassoc (attr := simp)]
lemma isoOfIsAffine_inv_over [IsAffine S] :
    (isoOfIsAffine n S).inv â‰« ğ”¸(n; S) â†˜ S = Spec.map (CommRingCat.ofHom C) â‰« S.isoSpec.inv :=
  pullback.lift_fst _ _ _

instance [IsAffine S] : IsAffine ğ”¸(n; S) := .of_isIso (isoOfIsAffine n S).hom

variable (n) in
/-- The affine space over an affine base is isomorphic to the spectrum of the polynomial ring. -/
def SpecIso (R : CommRingCat.{max u v}) :
    ğ”¸(n; Spec R) â‰… Spec <| .of <| MvPolynomial n R :=
  isoOfIsAffine _ _ â‰ªâ‰« Scheme.Spec.mapIso (MvPolynomial.mapEquiv _
    (Scheme.Î“SpecIso R).symm.commRingCatIsoToRingEquiv).toCommRingCatIso.op

@[simp]
lemma SpecIso_hom_appTop (R : CommRingCat.{max u v}) :
    (SpecIso n R).hom.appTop = (Scheme.Î“SpecIso _).hom â‰«
      CommRingCat.ofHom (evalâ‚‚Hom ((Scheme.Î“SpecIso _).inv â‰«
        (ğ”¸(n; Spec R) â†˜ Spec R).appTop).hom (coord (Spec R))) := by
  simp only [SpecIso, Iso.trans_hom, Functor.mapIso_hom, Iso.op_hom,
    Scheme.Spec_map, Quiver.Hom.unop_op, TopologicalSpace.Opens.map_top, Scheme.Hom.comp_app,
    isoOfIsAffine_hom_appTop, Scheme.Î“SpecIso_naturality_assoc]
  congr 1
  ext : 1
  apply ringHom_ext'
  Â· ext; simp
  Â· simp

@[simp]
lemma SpecIso_inv_appTop_coord (R : CommRingCat.{max u v}) (i) :
    (SpecIso n R).inv.appTop (coord _ i) = (Scheme.Î“SpecIso (.of _)).inv (.X i) := by
  simp only [SpecIso, Iso.trans_inv, Functor.mapIso_inv, Iso.op_inv, Scheme.Spec_map,
    Quiver.Hom.unop_op, TopologicalSpace.Opens.map_top, Scheme.Hom.comp_app, CommRingCat.comp_apply]
  rw [isoOfIsAffine_inv_appTop_coord, â† CommRingCat.comp_apply, â† Scheme.Î“SpecIso_inv_naturality,
      CommRingCat.comp_apply]
  congr 1
  exact map_X _ _

@[reassoc (attr := simp)]
lemma SpecIso_inv_over (R : CommRingCat.{max u v}) :
    (SpecIso n R).inv â‰« ğ”¸(n; Spec R) â†˜ Spec R = Spec.map (CommRingCat.ofHom C) := by
  simp only [SpecIso, Iso.trans_inv, Functor.mapIso_inv, Iso.op_inv, Scheme.Spec_map,
    Quiver.Hom.unop_op, Category.assoc, isoOfIsAffine_inv_over, Scheme.isoSpec_Spec_inv,
    â† Spec.map_comp]
  congr 1
  rw [Iso.inv_comp_eq]
  ext : 2
  exact map_C _ _

section functorial

variable (n) in
/-- `ğ”¸(n; S)` is functorial w.r.t. `S`. -/
def map {S T : Scheme.{max u v}} (f : S âŸ¶ T) : ğ”¸(n; S) âŸ¶ ğ”¸(n; T) :=
  homOfVector (ğ”¸(n; S) â†˜ S â‰« f) (coord S)

@[reassoc (attr := simp)]
lemma map_over {S T : Scheme.{max u v}} (f : S âŸ¶ T) : map n f â‰« ğ”¸(n; T) â†˜ T = ğ”¸(n; S) â†˜ S â‰« f :=
  pullback.lift_fst _ _ _

@[simp]
lemma map_appTop_coord {S T : Scheme.{max u v}} (f : S âŸ¶ T) (i) :
    (map n f).appTop (coord T i) = coord S i :=
  homOfVector_appTop_coord _ _ _

@[reassoc (attr := simp)]
lemma map_toSpecMvPoly {S T : Scheme.{max u v}} (f : S âŸ¶ T) :
    map n f â‰« toSpecMvPoly n T = toSpecMvPoly n S := by
  apply (toSpecMvPolyIntEquiv _).injective
  ext i
  rw [toSpecMvPolyIntEquiv_comp, â† coord, map_appTop_coord, coord]

@[simp]
lemma map_id : map n (ğŸ™ S) = ğŸ™ ğ”¸(n; S) := by
  ext1 <;> simp

@[reassoc, simp]
lemma map_comp {S S' S'' : Scheme} (f : S âŸ¶ S') (g : S' âŸ¶ S'') :
    map n (f â‰« g) = map n f â‰« map n g := by
  ext1
  Â· simp
  Â· simp

lemma map_SpecMap {R S : CommRingCat.{max u v}} (Ï† : R âŸ¶ S) :
    map n (Spec.map Ï†) =
      (SpecIso n S).hom â‰« Spec.map (CommRingCat.ofHom (MvPolynomial.map Ï†.hom)) â‰«
        (SpecIso n R).inv := by
  rw [â† Iso.inv_comp_eq]
  ext1
  Â· simp only [map_over, Category.assoc, SpecIso_inv_over, SpecIso_inv_over_assoc,
      â† Spec.map_comp, â† CommRingCat.ofHom_comp]
    rw [map_comp_C, CommRingCat.ofHom_comp, CommRingCat.ofHom_hom]
  Â· simp only [TopologicalSpace.Opens.map_top, Scheme.Hom.comp_app, CommRingCat.comp_apply]
    conv_lhs => enter[2]; tactic => exact map_appTop_coord _ _
    conv_rhs => enter[2]; tactic => exact SpecIso_inv_appTop_coord _ _
    rw [SpecIso_inv_appTop_coord, â† CommRingCat.comp_apply, â† Scheme.Î“SpecIso_inv_naturality,
        CommRingCat.comp_apply, ConcreteCategory.hom_ofHom, map_X]

@[deprecated (since := "2025-10-07")] alias map_Spec_map := map_SpecMap

/-- The map between affine spaces over affine bases is
isomorphic to the natural map between polynomial rings. -/
def mapSpecMap {R S : CommRingCat.{max u v}} (Ï† : R âŸ¶ S) :
    Arrow.mk (map n (Spec.map Ï†)) â‰…
      Arrow.mk (Spec.map (CommRingCat.ofHom (MvPolynomial.map (Ïƒ := n) Ï†.hom))) :=
  Arrow.isoMk (SpecIso n S) (SpecIso n R) (by have := (SpecIso n R).inv_hom_id; simp [map_SpecMap])

lemma isPullback_map {S T : Scheme.{max u v}} (f : S âŸ¶ T) :
    IsPullback (map n f) (ğ”¸(n; S) â†˜ S) (ğ”¸(n; T) â†˜ T) f := by
  refine (IsPullback.paste_horiz_iff (.flip <| .of_hasPullback _ _) (map_over f)).mp ?_
  simp only [terminal.comp_from, ]
  convert (IsPullback.of_hasPullback _ _).flip
  rw [â† toSpecMvPoly, â† toSpecMvPoly, map_toSpecMvPoly]

/-- `ğ”¸(n; S)` is functorial w.r.t. `n`. -/
def reindex {n m : Type v} (i : m â†’ n) (S : Scheme.{max u v}) : ğ”¸(n; S) âŸ¶ ğ”¸(m; S) :=
  homOfVector (ğ”¸(n; S) â†˜ S) (coord S âˆ˜ i)

@[simp, reassoc]
lemma reindex_over {n m : Type v} (i : m â†’ n) (S : Scheme.{max u v}) :
    reindex i S â‰« ğ”¸(m; S) â†˜ S = ğ”¸(n; S) â†˜ S :=
  pullback.lift_fst _ _ _

@[simp]
lemma reindex_appTop_coord {n m : Type v} (i : m â†’ n) (S : Scheme.{max u v}) (j : m) :
    (reindex i S).appTop (coord S j) = coord S (i j) :=
  homOfVector_appTop_coord _ _ _

@[simp]
lemma reindex_id : reindex id S = ğŸ™ ğ”¸(n; S) := by
  ext1 <;> simp

@[simp, reassoc]
lemma reindex_comp {nâ‚ nâ‚‚ nâ‚ƒ : Type v} (i : nâ‚ â†’ nâ‚‚) (j : nâ‚‚ â†’ nâ‚ƒ) (S : Scheme.{max u v}) :
    reindex (j âˆ˜ i) S = reindex j S â‰« reindex i S := by
  have Hâ‚ : reindex (j âˆ˜ i) S â‰« ğ”¸(nâ‚; S) â†˜ S = (reindex j S â‰« reindex i S) â‰« ğ”¸(nâ‚; S) â†˜ S := by
    simp
  have Hâ‚‚ (k) : (reindex (j âˆ˜ i) S).appTop (coord S k) =
      (reindex j S).appTop ((reindex i S).appTop (coord S k)) := by
    rw [reindex_appTop_coord, reindex_appTop_coord, reindex_appTop_coord]
    rfl
  exact hom_ext Hâ‚ Hâ‚‚

@[reassoc (attr := simp)]
lemma map_reindex {nâ‚ nâ‚‚ : Type v} (i : nâ‚ â†’ nâ‚‚) {S T : Scheme.{max u v}} (f : S âŸ¶ T) :
    map nâ‚‚ f â‰« reindex i T = reindex i S â‰« map nâ‚ f := by
  apply hom_ext <;> simp

/-- The affine space as a functor. -/
@[simps]
def functor : (Type v)áµ’áµ– â¥¤ Scheme.{max u v} â¥¤ Scheme.{max u v} where
  obj n := { obj := AffineSpace n.unop, map := map n.unop, map_id := map_id, map_comp := map_comp }
  map {n m} i := { app := reindex i.unop, naturality := fun _ _ â†¦ map_reindex i.unop }
  map_id n := by ext: 2; exact reindex_id _
  map_comp f g := by ext: 2; dsimp; exact reindex_comp _ _ _

end functorial
section instances

instance : IsAffineHom (ğ”¸(n; S) â†˜ S) := MorphismProperty.pullback_fst _ _ inferInstance

instance : Surjective (ğ”¸(n; S) â†˜ S) := MorphismProperty.pullback_fst _ _ <| by
  have := isIso_of_isTerminal specULiftZIsTerminal terminalIsTerminal (terminal.from _)
  rw [â† terminal.comp_from (Spec.map (CommRingCat.ofHom C)),
    MorphismProperty.cancel_right_of_respectsIso (P := @Surjective)]
  exact âŸ¨MvPolynomial.comap_C_surjectiveâŸ©

instance [Finite n] : LocallyOfFinitePresentation (ğ”¸(n; S) â†˜ S) :=
  MorphismProperty.pullback_fst _ _ <| by
  have := isIso_of_isTerminal specULiftZIsTerminal.{max u v} terminalIsTerminal (terminal.from _)
  rw [â† terminal.comp_from (Spec.map (CommRingCat.ofHom C)),
    MorphismProperty.cancel_right_of_respectsIso (P := @LocallyOfFinitePresentation),
    HasRingHomProperty.Spec_iff (P := @LocallyOfFinitePresentation), RingHom.FinitePresentation]
  convert (inferInstanceAs (Algebra.FinitePresentation (ULift â„¤) â„¤[n]))
  exact Algebra.algebra_ext _ _ fun _ â†¦ rfl

lemma isOpenMap_over : IsOpenMap (ğ”¸(n; S) â†˜ S) := by
  change topologically @IsOpenMap _
  wlog hS : âˆƒ R, S = Spec R
  Â· refine (IsZariskiLocalAtTarget.iff_of_openCover
      (P := topologically @IsOpenMap) S.affineCover).mpr ?_
    intro i
    have := this (n := n) (S.affineCover.X i) âŸ¨_, rflâŸ©
    rwa [â† (isPullback_map (n := n)  (S.affineCover.f i)).isoPullback_hom_snd,
      MorphismProperty.cancel_left_of_respectsIso (P := topologically @IsOpenMap)] at this
  obtain âŸ¨R, rflâŸ© := hS
  rw [â† MorphismProperty.cancel_left_of_respectsIso (P := topologically @IsOpenMap)
    (SpecIso n R).inv, SpecIso_inv_over]
  exact MvPolynomial.isOpenMap_comap_C

open MorphismProperty in
instance [IsEmpty n] : IsIso (ğ”¸(n; S) â†˜ S) := pullback_fst
    (P := isomorphisms _) _ _ <| by
  rw [â† terminal.comp_from (Spec.map (CommRingCat.ofHom C))]
  apply IsStableUnderComposition.comp_mem
  Â· rw [HasAffineProperty.iff_of_isAffine (P := isomorphisms _), â† isomorphisms,
      â† arrow_mk_iso_iff (isomorphisms _) (arrowIsoÎ“SpecOfIsAffine _)]
    exact âŸ¨inferInstance, (ConcreteCategory.isIso_iff_bijective _).mpr
      âŸ¨C_injective n _, C_surjective _âŸ©âŸ©
  Â· exact isIso_of_isTerminal specULiftZIsTerminal terminalIsTerminal (terminal.from _)

lemma isIntegralHom_over_iff_isEmpty : IsIntegralHom (ğ”¸(n; S) â†˜ S) â†” IsEmpty S âˆ¨ IsEmpty n := by
  constructor
  Â· intro h
    cases isEmpty_or_nonempty S
    Â· exact .inl â€¹_â€º
    refine .inr ?_
    wlog hS : âˆƒ R, S = Spec R
    Â· obtain âŸ¨xâŸ© := â€¹Nonempty Sâ€º
      obtain âŸ¨y, hyâŸ© := S.affineCover.covers x
      exact this (S.affineCover.X _) (MorphismProperty.IsStableUnderBaseChange.of_isPullback
        (isPullback_map (S.affineCover.f _)) h) âŸ¨yâŸ© âŸ¨_, rflâŸ©
    obtain âŸ¨R, rflâŸ© := hS
    have : Nontrivial R := (subsingleton_or_nontrivial R).resolve_left fun H â†¦
        not_isEmpty_of_nonempty (Spec R) (inferInstanceAs (IsEmpty (PrimeSpectrum R)))
    constructor
    intro i
    have := RingHom.toMorphismProperty_respectsIso_iff.mp RingHom.isIntegral_respectsIso.{max u v}
    rw [â† MorphismProperty.cancel_left_of_respectsIso @IsIntegralHom (SpecIso n R).inv,
      SpecIso_inv_over, HasAffineProperty.iff_of_isAffine (P := @IsIntegralHom)] at h
    obtain âŸ¨p : Polynomial R, hp, hp'âŸ© :=
      (MorphismProperty.arrow_mk_iso_iff (RingHom.toMorphismProperty RingHom.IsIntegral)
        (arrowIsoÎ“SpecOfIsAffine _)).mpr h.2 (X i)
    have : (rename fun _ â†¦ i).comp (pUnitAlgEquiv.{_, v} _).symm.toAlgHom p = 0 := by
      simp [â† hp', â† algebraMap_eq]
    rw [AlgHom.comp_apply, map_eq_zero_iff _ (rename_injective _ (fun _ _ _ â†¦ rfl))] at this
    simp only [AlgEquiv.toAlgHom_eq_coe, AlgHom.coe_coe, EmbeddingLike.map_eq_zero_iff] at this
    simp [this] at hp
  Â· rintro (_ | _) <;> infer_instance

lemma not_isIntegralHom [Nonempty S] [Nonempty n] : Â¬ IsIntegralHom (ğ”¸(n; S) â†˜ S) := by
  simp [isIntegralHom_over_iff_isEmpty]

lemma spec_le_iff (R : CommRingCat) (p q : Spec R) : p â‰¤ q â†” q.asIdeal â‰¤ p.asIdeal := by
  aesop (add simp PrimeSpectrum.le_iff_specializes)

/--
One should bear this equality in mind when breaking the `Spec R/ PrimeSpectrum R` abstraction
boundary, since these instances are not definitionally equal.
-/
example (R : CommRingCat) :
    inferInstance (Î± := Preorder (Spec R)) = inferInstance (Î± := Preorder (PrimeSpectrum R)áµ’áµˆ) := by
  aesop (add simp spec_le_iff)

end instances

end AffineSpace

end AlgebraicGeometry
