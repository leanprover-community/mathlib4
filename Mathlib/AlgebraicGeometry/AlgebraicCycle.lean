/-
Copyright (c) 2025 Raphael Douglas Giles. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Raphael Douglas Giles
-/
import Mathlib.AlgebraicGeometry.Fiber
import Mathlib.AlgebraicGeometry.Morphisms.Proper
import Mathlib.Combinatorics.Quiver.ReflQuiver
import Mathlib.LinearAlgebra.FreeModule.StrongRankCondition
import Mathlib.Topology.LocallyFinsupp

/-!
# Algebraic Cycles

We define algebraic cycles on a scheme `X` to be functions `c : X ‚Üí ‚Ñ§` whose support is
locally finite.
-/

open AlgebraicGeometry Set Order LocallyRingedSpace Topology TopologicalSpace
  CategoryTheory

universe u v
variable (R : Type*)
         [CommRing R]
         (i : ‚Ñï)
         {X Y : Scheme.{u}}

/--
Algebraic cycle on a scheme X.

Note I am not certain that this should be an abbrev. I'm also not sure if these definitions
should instead directly be about Function.locallyFinsuppWithin
-/
abbrev AlgebraicCycle (X : Scheme.{u}) := Function.locallyFinsuppWithin (‚ä§ : Set X) ‚Ñ§

namespace AlgebraicCycle

/--
Proposition saying whether a cycle is of pure dimension `d`.
-/
def IsHomogeneous (d : ‚Ñï‚àû) (c : AlgebraicCycle X) : Prop := ‚àÄ x ‚àà c.support, height x = d

/--
Subgroup of cycles of pure dimension `d`.
-/
def HomogeneousAddSubgroup (X : Scheme) (d : ‚Ñï‚àû) : AddSubgroup (AlgebraicCycle X) where
  carrier := {c : AlgebraicCycle X | IsHomogeneous d c}
  add_mem' c‚ÇÅ c‚ÇÇ := by
    rename_i a b
    simp_all only [IsHomogeneous, top_eq_univ, Function.mem_support, ne_eq, mem_setOf_eq,
      Function.locallyFinsuppWithin.coe_add, Pi.add_apply]
    intro x hx
    specialize c‚ÇÅ x
    specialize c‚ÇÇ x
    have : ¬¨ a x = 0 ‚à® ¬¨ b x = 0 := by omega
    obtain h | h := this
    ¬∑ exact c‚ÇÅ h
    ¬∑ exact c‚ÇÇ h
  zero_mem' := by simp [IsHomogeneous]
  neg_mem' c := by simp_all [IsHomogeneous]

/--
Homogeneous part of dimension `d` of an algebraic cycle `c`.
-/
noncomputable
def homogeneousProjection (c : AlgebraicCycle X) (d : ‚Ñï‚àû) : HomogeneousAddSubgroup X d where
  val := {
    toFun x := if height x = d then c x else 0
    supportWithinDomain' := by simp
    supportLocallyFiniteWithinDomain' z hz := by
      choose t ht using c.supportLocallyFiniteWithinDomain' z
      use t hz
      specialize ht hz
      refine ‚ü®ht.1, ?_‚ü©
      have := ht.2
      apply Finite.subset this
      refine inter_subset_inter (fun ‚¶Éa‚¶Ñ a ‚Ü¶ a) (Function.support_subset_iff'.mpr ?_)
      intro x hx
      simp only [top_eq_univ, Function.mem_support, ne_eq, Decidable.not_not,
        ite_eq_right_iff] at hx ‚ä¢
      exact fun _ ‚Ü¶ hx
  }
  property := by
    simp only [top_eq_univ, HomogeneousAddSubgroup, IsHomogeneous, Function.mem_support, ne_eq,
      AddSubgroup.mem_mk, mem_setOf_eq]
    intro x hx
    have : ¬¨ (if height x = d then c x else 0) = 0 := hx
    aesop

variable (f : X ‚ü∂ Y)
         (c : AlgebraicCycle X)
         (x : X)
         (z : Y)

/--
The cycle containing a single point with a chosen coefficient
-/
noncomputable
def single (coeff : ‚Ñ§) : AlgebraicCycle X where
  toFun := Set.indicator {x} (Function.const X coeff)
  supportWithinDomain' := by simp
  supportLocallyFiniteWithinDomain' z hz :=
    ‚ü®‚ä§, ‚ü®Filter.univ_mem' fun a ‚Ü¶ trivial, by simp [‚Üê Function.const_def, toFinite]‚ü©‚ü©

/--
Implementation detail for the pushforward; the support of a cycle on X intersected with the preimage
of a point z : Y along a morphism f : X ‚ü∂ Y.
-/
def preimageSupport : Set X :=
  f.base ‚Åª¬π' {z} ‚à© c.support

/--
Implementation detail for the pushforward; the support of a cycle on X intersected with the preimage
of a point z : Y along a quasicompact morphism f : X ‚ü∂ Y is finite.
-/
lemma preimageSupport_finite [qf : QuasiCompact f] :
 (preimageSupport f c z).Finite :=
 supportLocallyFiniteWithin_top_inter_compact_finite c.supportLocallyFiniteWithinDomain' <|
  QuasiCompact.isCompact_preimage_singleton f z

/--
Degree of `f` at a point `x` is defined to be the degree of the associated field extension
from `Œ∫(f x)` to `Œ∫(x)`. We return a default value of zero when this degree is either infinite
or undefined.
-/
noncomputable
def _root_.AlgebraicGeometry.LocallyRingedSpace.Hom.degree : ‚Ñï := @Module.finrank
    (IsLocalRing.ResidueField (Y.presheaf.stalk (f.base x)))
    (IsLocalRing.ResidueField (X.presheaf.stalk x))
    (by infer_instance)
    (by infer_instance)
    (by have :=
      RingHom.toAlgebra (IsLocalRing.ResidueField.map (f.stalkMap x).hom);exact Algebra.toModule)


open Classical in
/--
Implementation detail for pushforward: function used to define the coefficient of the pushforward
of a cycle `c` at a point `z = f x`, as in stacks 02R3.

Note: I'm not entirely sure if the case distinction here (and hence this definition) is necessary,
since the degree alread has a default value of zero whenever the degree of the field extension is
not finite.
-/
noncomputable
def mapAux {Y : Scheme} (f : X ‚ü∂ Y) (x : X) : ‚Ñ§ :=
  if height x = height (f.base x) then Hom.degree f x else 0

/--
The pushforward of an algebraic cycle has locally finite support.

Note that while this could be part of the definition of map, we experienced significant performance
issues when instead writing this definition in the `supportLocallyFiniteWithinDomain'` field of the
`map` definition.

I feel the proof here is a bit too long, but I'm a little unsure of how I should shorten it.
-/
lemma map_locally_finite {Y : Scheme}
  (f : X ‚ü∂ Y) [qc : QuasiCompact f] (c : AlgebraicCycle X) :
  ‚àÄ z : Y, ‚àÉ t ‚àà ùìù z, (t ‚à© Function.support fun z ‚Ü¶
  ‚àë x ‚àà (preimageSupport_finite f c z).toFinset, (c x) * mapAux f x).Finite := by
  intro y
  obtain ‚ü®W, hW‚ü© := exists_isAffineOpen_mem_and_subset (x := y) (U := ‚ä§) (by aesop)
  have cpct : IsCompact (f.base ‚Åª¬π' W) := qc.1 W.carrier W.is_open' <|
     AlgebraicGeometry.IsAffineOpen.isCompact hW.1
  use W
  refine ‚ü®IsOpen.mem_nhds (Opens.isOpen W) hW.2.1, ?_‚ü©

  have pbfinite : (f.base ‚Åª¬π' W ‚à© Function.support c).Finite :=
   supportLocallyFiniteWithin_top_inter_compact_finite c.supportLocallyFiniteWithinDomain' cpct

  suffices (W.carrier ‚à© {z : Y | (preimageSupport f c z).Nonempty}).Finite by
      apply Finite.subset this
      apply inter_subset_inter Set.Subset.rfl
      intro x
      simp only [top_eq_univ, Function.mem_support, ne_eq, mem_setOf_eq]
      contrapose!
      intro aux
      rw [Finset.sum_eq_zero]
      intro x hx
      simp_all

  have : W.carrier ‚à© {z | (preimageSupport f c z).Nonempty} ‚äÜ
    f.base '' (f.base ‚Åª¬π' ((W.carrier ‚à© {z | (preimageSupport f c z).Nonempty})) ‚à© c.support) := by
    intro a ha
    rw [image_preimage_inter]
    suffices a ‚àà f.base '' c.support from mem_inter ha this
    have := ha.2.some_mem
    simp only [preimageSupport, top_eq_univ, mem_inter_iff, mem_preimage, mem_singleton_iff,
      Function.mem_support, ne_eq, mem_image] at this ‚ä¢
    exact ‚ü®ha.2.some, this.symm‚ü©

  refine Finite.subset (Finite.image _ ?_) this
  rw[preimage_inter]
  have : f.base ‚Åª¬π' W.carrier ‚à© f.base ‚Åª¬π' {z | (preimageSupport f c z).Nonempty} ‚à© c.support ‚äÜ
      f.base ‚Åª¬π' W.carrier ‚à© (‚ãÉ z : Y, preimageSupport f c z) := by
    intro p hp
    simp only [Opens.carrier_eq_coe, preimageSupport, top_eq_univ, preimage_setOf_eq, mem_inter_iff,
      mem_preimage, SetLike.mem_coe, mem_setOf_eq, Function.mem_support, ne_eq, mem_iUnion,
      mem_singleton_iff, exists_and_right, exists_eq', true_and] at hp ‚ä¢
    exact ‚ü®hp.1.1, hp.2‚ü©

  apply Finite.subset _ this
  rw[inter_iUnion]
  simp[preimageSupport]
  suffices (‚ãÉ i : Y, f.base ‚Åª¬π' W.carrier ‚à© c.support).Finite by
    apply Finite.subset this
    simp only [Opens.carrier_eq_coe, top_eq_univ, iUnion_subset_iff]
    intro y x hx
    simp only [mem_inter_iff, mem_preimage, SetLike.mem_coe, mem_singleton_iff,
      Function.mem_support, ne_eq, mem_iUnion, exists_and_left, exists_const_iff] at hx ‚ä¢
    exact ‚ü®hx.1, ‚ü®Nonempty.intro y, hx.2.2‚ü©‚ü©

  suffices (f.base ‚Åª¬π' W.carrier ‚à© c.support).Finite by
    apply Finite.subset this
    intro a ha
    simp only [Opens.carrier_eq_coe, top_eq_univ, mem_iUnion, mem_inter_iff, mem_preimage,
      SetLike.mem_coe, Function.mem_support, ne_eq, exists_and_left, exists_const_iff] at ha ‚ä¢
    exact ‚ü®ha.1, ha.2.2‚ü©

  exact pbfinite

/--
The pushforward of an algebraic cycle by a quasicompact morphism.

Note that usually the pushforward is only defined for proper morphisms, and indeed we will need
properness to prove that the pushforward preserves rational equivalence.
-/
noncomputable
def map {Y : Scheme.{u}} (f : X ‚ü∂ Y) [qc : QuasiCompact f] (c : AlgebraicCycle X) : AlgebraicCycle Y
    where
  toFun z := (‚àë x ‚àà (preimageSupport_finite f c z).toFinset, (c x) * mapAux f x)
  supportWithinDomain' := by simp
  supportLocallyFiniteWithinDomain' := fun z a ‚Ü¶ map_locally_finite f c z

/--
Pushforward preserves cycles of pure dimension `d`.
-/
noncomputable
def map_homogeneneous {Y : Scheme.{u}} {d : ‚Ñï‚àû} (f : X ‚ü∂ Y) [qc : QuasiCompact f]
  (c : HomogeneousAddSubgroup X d) : HomogeneousAddSubgroup Y d where
    val := map f c
    property := by
      simp[HomogeneousAddSubgroup, IsHomogeneous]
      intro y hy
      have : ¬¨ (map f c).toFun y = 0 := hy
      simp only [top_eq_univ, map, preimageSupport, mapAux, mul_ite, mul_zero] at this
      obtain ‚ü®x, hx‚ü© := Finset.exists_ne_zero_of_sum_ne_zero this
      simp only [Finite.mem_toFinset, mem_inter_iff, mem_preimage, mem_singleton_iff,
        Function.mem_support, ne_eq, ite_eq_right_iff, mul_eq_zero, Int.natCast_eq_zero,
        Classical.not_imp, not_or] at hx
      have : height x = d := c.2 x hx.1.2
      aesop

/--
The pushforward of `c` along the identity morphism is `c`.
-/
@[simp]
lemma map_id (c : AlgebraicCycle X) :
    map (ùüô X) c = c := by
   ext z
   have : (c z ‚â† 0 ‚àß (preimageSupport_finite (ùüô X) c z).toFinset = {z}) ‚à®
          (c z = 0 ‚àß (preimageSupport_finite (ùüô X) c z).toFinset = ‚àÖ) := by
    simp[preimageSupport_finite, preimageSupport, Finite.toFinset]
    refine Or.elim (em (c z = 0)) (fun o ‚Ü¶ Or.inr o) (fun o ‚Ü¶ Or.inl ‚ü®o, Finset.ext (fun a ‚Ü¶ ?_)‚ü©)
    simp only [mem_toFinset, mem_inter_iff, mem_singleton_iff, Function.mem_support, ne_eq,
      Finset.mem_singleton, and_iff_left_iff_imp]
    rintro rfl
    assumption
   suffices (map (ùüô X) c).toFun z = c.toFun z from this
   obtain h | h := this
   all_goals simp only [top_eq_univ, map, mapAux, Scheme.id.base, TopCat.hom_id,
               ContinuousMap.id_apply, ‚ÜìreduceIte]
             rw[h.2]
             simp only [Hom.degree, Scheme.id.base, TopCat.hom_id, ContinuousMap.id_apply,
               Scheme.stalkMap_id, CommRingCat.hom_id, IsLocalRing.ResidueField.map_id,
               Module.finrank_self, Nat.cast_one, mul_one, Finset.sum_singleton]
   ¬∑ rfl
   ¬∑ exact h.1.symm

end AlgebraicCycle
