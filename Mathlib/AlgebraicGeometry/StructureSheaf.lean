/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Kim Morrison
-/
import Mathlib.Algebra.Category.Ring.Colimits
import Mathlib.Algebra.Category.Ring.Instances
import Mathlib.Algebra.Category.Ring.Limits
import Mathlib.Algebra.Ring.Subring.Basic
import Mathlib.RingTheory.Localization.AtPrime.Basic
import Mathlib.RingTheory.Spectrum.Prime.Topology
import Mathlib.Topology.Sheaves.LocalPredicate

/-!
# The structure sheaf on `PrimeSpectrum R`.

We define the structure sheaf on `TopCat.of (PrimeSpectrum R)`, for a commutative ring `R` and prove
basic properties about it. We define this as a subsheaf of the sheaf of dependent functions into the
localizations, cut out by the condition that the function must be locally equal to a ratio of
elements of `R`.

Because the condition "is equal to a fraction" passes to smaller open subsets,
the subset of functions satisfying this condition is automatically a subpresheaf.
Because the condition "is locally equal to a fraction" is local,
it is also a subsheaf.

(It may be helpful to refer back to `Mathlib/Topology/Sheaves/SheafOfFunctions.lean`,
where we show that dependent functions into any type family form a sheaf,
and also `Mathlib/Topology/Sheaves/LocalPredicate.lean`, where we characterise the predicates
which pick out sub-presheaves and sub-sheaves of these sheaves.)

We also set up the ring structure, obtaining
`structureSheaf : Sheaf CommRingCat (PrimeSpectrum.Top R)`.

We then construct two basic isomorphisms, relating the structure sheaf to the underlying ring `R`.
First, `StructureSheaf.stalkIso` gives an isomorphism between the stalk of the structure sheaf
at a point `p` and the localization of `R` at the prime ideal `p`. Second,
`StructureSheaf.basicOpenIso` gives an isomorphism between the structure sheaf on `basicOpen f`
and the localization of `R` at the submonoid of powers of `f`.

## References

* [Robin Hartshorne, *Algebraic Geometry*][Har77]


-/


universe u

noncomputable section

variable (R : Type u) [CommRing R]

open TopCat

open TopologicalSpace

open CategoryTheory

open Opposite

namespace AlgebraicGeometry

/-- The prime spectrum, just as a topological space.
-/
def PrimeSpectrum.Top : TopCat :=
  TopCat.of (PrimeSpectrum R)

namespace StructureSheaf

/-- The type family over `PrimeSpectrum R` consisting of the localization over each point.
-/
def Localizations (P : PrimeSpectrum.Top R) : Type u :=
  Localization.AtPrime P.asIdeal

instance commRingLocalizations (P : PrimeSpectrum.Top R) : CommRing <| Localizations R P :=
  inferInstanceAs <| CommRing <| Localization.AtPrime P.asIdeal

instance localRingLocalizations (P : PrimeSpectrum.Top R) : IsLocalRing <| Localizations R P :=
  inferInstanceAs <| IsLocalRing <| Localization.AtPrime P.asIdeal

instance (P : PrimeSpectrum.Top R) : Inhabited (Localizations R P) :=
  ‚ü®1‚ü©

instance (U : Opens (PrimeSpectrum.Top R)) (x : U) : Algebra R (Localizations R x) :=
  inferInstanceAs <| Algebra R (Localization.AtPrime x.1.asIdeal)

instance (U : Opens (PrimeSpectrum.Top R)) (x : U) :
    IsLocalization.AtPrime (Localizations R x) (x : PrimeSpectrum.Top R).asIdeal :=
  Localization.isLocalization

variable {R}

/-- The predicate saying that a dependent function on an open `U` is realised as a fixed fraction
`r / s` in each of the stalks (which are localizations at various prime ideals).
-/
def IsFraction {U : Opens (PrimeSpectrum.Top R)} (f : ‚àÄ x : U, Localizations R x) : Prop :=
  ‚àÉ r s : R, ‚àÄ x : U, s ‚àâ x.1.asIdeal ‚àß f x * algebraMap _ _ s = algebraMap _ _ r

theorem IsFraction.eq_mk' {U : Opens (PrimeSpectrum.Top R)} {f : ‚àÄ x : U, Localizations R x}
    (hf : IsFraction f) :
    ‚àÉ r s : R,
      ‚àÄ x : U,
        ‚àÉ hs : s ‚àâ x.1.asIdeal,
          f x =
            IsLocalization.mk' (Localization.AtPrime _) r
              (‚ü®s, hs‚ü© : (x : PrimeSpectrum.Top R).asIdeal.primeCompl) := by
  rcases hf with ‚ü®r, s, h‚ü©
  refine ‚ü®r, s, fun x => ‚ü®(h x).1, (IsLocalization.mk'_eq_iff_eq_mul.mpr ?_).symm‚ü©‚ü©
  exact (h x).2.symm

variable (R)

/-- The predicate `IsFraction` is "prelocal",
in the sense that if it holds on `U` it holds on any open subset `V` of `U`.
-/
def isFractionPrelocal : PrelocalPredicate (Localizations R) where
  pred {_} f := IsFraction f
  res := by rintro V U i f ‚ü®r, s, w‚ü©; exact ‚ü®r, s, fun x => w (i x)‚ü©

/-- We will define the structure sheaf as
the subsheaf of all dependent functions in `Œ† x : U, Localizations R x`
consisting of those functions which can locally be expressed as a ratio of
(the images in the localization of) elements of `R`.

Quoting Hartshorne:

For an open set $U ‚äÜ Spec A$, we define $ùí™(U)$ to be the set of functions
$s : U ‚Üí ‚®Ü_{ùî≠ ‚àà U} A_ùî≠$, such that $s(ùî≠) ‚àà A_ùî≠$ for each $ùî≠$,
and such that $s$ is locally a quotient of elements of $A$:
to be precise, we require that for each $ùî≠ ‚àà U$, there is a neighborhood $V$ of $ùî≠$,
contained in $U$, and elements $a, f ‚àà A$, such that for each $ùîÆ ‚àà V, f ‚àâ ùîÆ$,
and $s(ùîÆ) = a/f$ in $A_ùîÆ$.

Now Hartshorne had the disadvantage of not knowing about dependent functions,
so we replace his circumlocution about functions into a disjoint union with
`Œ† x : U, Localizations x`.
-/
def isLocallyFraction : LocalPredicate (Localizations R) :=
  (isFractionPrelocal R).sheafify

@[simp]
theorem isLocallyFraction_pred {U : Opens (PrimeSpectrum.Top R)} (f : ‚àÄ x : U, Localizations R x) :
    (isLocallyFraction R).pred f =
      ‚àÄ x : U,
        ‚àÉ (V : _) (_ : x.1 ‚àà V) (i : V ‚ü∂ U),
          ‚àÉ r s : R,
            ‚àÄ y : V, s ‚àâ y.1.asIdeal ‚àß f (i y : U) * algebraMap _ _ s = algebraMap _ _ r :=
  rfl

/-- The functions satisfying `isLocallyFraction` form a subring.
-/
def sectionsSubring (U : (Opens (PrimeSpectrum.Top R))·µí·µñ) :
    Subring (‚àÄ x : U.unop, Localizations R x) where
  carrier := { f | (isLocallyFraction R).pred f }
  zero_mem' := by
    refine fun x => ‚ü®unop U, x.2, ùüô _, 0, 1, fun y => ‚ü®?_, ?_‚ü©‚ü©
    ¬∑ rw [‚Üê Ideal.ne_top_iff_one]; exact y.1.isPrime.1
    ¬∑ simp
  one_mem' := by
    refine fun x => ‚ü®unop U, x.2, ùüô _, 1, 1, fun y => ‚ü®?_, ?_‚ü©‚ü©
    ¬∑ rw [‚Üê Ideal.ne_top_iff_one]; exact y.1.isPrime.1
    ¬∑ simp
  add_mem' := by
    intro a b ha hb x
    rcases ha x with ‚ü®Va, ma, ia, ra, sa, wa‚ü©
    rcases hb x with ‚ü®Vb, mb, ib, rb, sb, wb‚ü©
    refine ‚ü®Va ‚äì Vb, ‚ü®ma, mb‚ü©, Opens.infLELeft _ _ ‚â´ ia, ra * sb + rb * sa, sa * sb, ?_‚ü©
    intro ‚ü®y, hy‚ü©
    rcases wa (Opens.infLELeft _ _ ‚ü®y, hy‚ü©) with ‚ü®nma, wa‚ü©
    rcases wb (Opens.infLERight _ _ ‚ü®y, hy‚ü©) with ‚ü®nmb, wb‚ü©
    fconstructor
    ¬∑ intro H; cases y.isPrime.mem_or_mem H <;> contradiction
    ¬∑ simp only [Opens.apply_mk, Pi.add_apply, RingHom.map_mul, add_mul, RingHom.map_add] at wa wb ‚ä¢
      grind
  neg_mem' := by
    intro a ha x
    rcases ha x with ‚ü®V, m, i, r, s, w‚ü©
    refine ‚ü®V, m, i, -r, s, ?_‚ü©
    intro y
    rcases w y with ‚ü®nm, w‚ü©
    fconstructor
    ¬∑ exact nm
    ¬∑ simp only [RingHom.map_neg, Pi.neg_apply]
      rw [‚Üê w]
      simp only [neg_mul]
  mul_mem' := by
    intro a b ha hb x
    rcases ha x with ‚ü®Va, ma, ia, ra, sa, wa‚ü©
    rcases hb x with ‚ü®Vb, mb, ib, rb, sb, wb‚ü©
    refine ‚ü®Va ‚äì Vb, ‚ü®ma, mb‚ü©, Opens.infLELeft _ _ ‚â´ ia, ra * rb, sa * sb, ?_‚ü©
    intro ‚ü®y, hy‚ü©
    rcases wa (Opens.infLELeft _ _ ‚ü®y, hy‚ü©) with ‚ü®nma, wa‚ü©
    rcases wb (Opens.infLERight _ _ ‚ü®y, hy‚ü©) with ‚ü®nmb, wb‚ü©
    fconstructor
    ¬∑ intro H; cases y.isPrime.mem_or_mem H <;> contradiction
    ¬∑ simp only [Opens.apply_mk, Pi.mul_apply, RingHom.map_mul] at wa wb ‚ä¢
      rw [‚Üê wa, ‚Üê wb]
      simp only [mul_left_comm, mul_assoc, mul_comm]

end StructureSheaf

open StructureSheaf

/-- The structure sheaf (valued in `Type`, not yet `CommRingCat`) is the subsheaf consisting of
functions satisfying `isLocallyFraction`.
-/
def structureSheafInType : Sheaf (Type u) (PrimeSpectrum.Top R) :=
  subsheafToTypes (isLocallyFraction R)

instance commRingStructureSheafInTypeObj (U : (Opens (PrimeSpectrum.Top R))·µí·µñ) :
    CommRing ((structureSheafInType R).1.obj U) :=
  (sectionsSubring R U).toCommRing

open PrimeSpectrum

/-- The structure presheaf, valued in `CommRingCat`, constructed by dressing up the `Type`-valued
structure presheaf.
-/
@[simps obj_carrier]
def structurePresheafInCommRing : Presheaf CommRingCat (PrimeSpectrum.Top R) where
  obj U := CommRingCat.of ((structureSheafInType R).1.obj U)
  map {_ _} i := CommRingCat.ofHom
    { toFun := (structureSheafInType R).1.map i
      map_zero' := rfl
      map_add' := fun _ _ => rfl
      map_one' := rfl
      map_mul' := fun _ _ => rfl }

/-- Some glue, verifying that the structure presheaf valued in `CommRingCat` agrees
with the `Type`-valued structure presheaf.
-/
def structurePresheafCompForget :
    structurePresheafInCommRing R ‚ãô forget CommRingCat ‚âÖ (structureSheafInType R).1 :=
  NatIso.ofComponents fun _ => Iso.refl _

open TopCat.Presheaf

/-- The structure sheaf on $Spec R$, valued in `CommRingCat`.

This is provided as a bundled `SheafedSpace` as `Spec.SheafedSpace R` later.
-/
def Spec.structureSheaf : Sheaf CommRingCat (PrimeSpectrum.Top R) :=
  ‚ü®structurePresheafInCommRing R,
    (-- We check the sheaf condition under `forget CommRingCat`.
          isSheaf_iff_isSheaf_comp
          _ _).mpr
      (isSheaf_of_iso (structurePresheafCompForget R).symm (structureSheafInType R).cond)‚ü©

open Spec (structureSheaf)

namespace StructureSheaf

@[simp]
theorem res_apply (U V : Opens (PrimeSpectrum.Top R)) (i : V ‚ü∂ U)
    (s : (structureSheaf R).1.obj (op U)) (x : V) :
    ((structureSheaf R).1.map i.op s).1 x = (s.1 (i x) :) :=
  rfl

/-

Notation in this comment

X = Spec R
OX = structure sheaf

In the following we construct an isomorphism between OX_p and R_p given any point p corresponding
to a prime ideal in R.

We do this via 8 steps:

1. def const (f g : R) (V) (hv : V ‚â§ D_g) : OX(V) [for api]
2. def toOpen (U) : R ‚ü∂ OX(U)
3. [2] def toStalk (p : Spec R) : R ‚ü∂ OX_p
4. [2] def toBasicOpen (f : R) : R_f ‚ü∂ OX(D_f)
5. [3] def localizationToStalk (p : Spec R) : R_p ‚ü∂ OX_p
6. def openToLocalization (U) (p) (hp : p ‚àà U) : OX(U) ‚ü∂ R_p
7. [6] def stalkToFiberRingHom (p : Spec R) : OX_p ‚ü∂ R_p
8. [5,7] def stalkIso (p : Spec R) : OX_p ‚âÖ R_p

In the square brackets we list the dependencies of a construction on the previous steps.

-/
/-- The section of `structureSheaf R` on an open `U` sending each `x ‚àà U` to the element
`f/g` in the localization of `R` at `x`. -/
def const (f g : R) (U : Opens (PrimeSpectrum.Top R))
    (hu : ‚àÄ x ‚àà U, g ‚àà (x : PrimeSpectrum.Top R).asIdeal.primeCompl) :
    (structureSheaf R).1.obj (op U) :=
  ‚ü®fun x => IsLocalization.mk' _ f ‚ü®g, hu x x.2‚ü©, fun x =>
    ‚ü®U, x.2, ùüô _, f, g, fun y => ‚ü®hu y y.2, IsLocalization.mk'_spec _ _ _‚ü©‚ü©‚ü©

@[simp]
theorem const_apply (f g : R) (U : Opens (PrimeSpectrum.Top R))
    (hu : ‚àÄ x ‚àà U, g ‚àà (x : PrimeSpectrum.Top R).asIdeal.primeCompl) (x : U) :
    (const R f g U hu).1 x =
      IsLocalization.mk' (Localization.AtPrime x.1.asIdeal) f ‚ü®g, hu x x.2‚ü© :=
  rfl

theorem const_apply' (f g : R) (U : Opens (PrimeSpectrum.Top R))
    (hu : ‚àÄ x ‚àà U, g ‚àà (x : PrimeSpectrum.Top R).asIdeal.primeCompl) (x : U)
    (hx : g ‚àà (x : PrimeSpectrum.Top R).asIdeal.primeCompl) :
    (const R f g U hu).1 x = IsLocalization.mk' _ f ‚ü®g, hx‚ü© :=
  rfl

theorem exists_const (U) (s : (structureSheaf R).1.obj (op U)) (x : PrimeSpectrum.Top R)
    (hx : x ‚àà U) :
    ‚àÉ (V : Opens (PrimeSpectrum.Top R)) (_ : x ‚àà V) (i : V ‚ü∂ U) (f g : R) (hg : _),
      const R f g V hg = (structureSheaf R).1.map i.op s :=
  let ‚ü®V, hxV, iVU, f, g, hfg‚ü© := s.2 ‚ü®x, hx‚ü©
  ‚ü®V, hxV, iVU, f, g, fun y hyV => (hfg ‚ü®y, hyV‚ü©).1,
    Subtype.ext <| funext fun y => IsLocalization.mk'_eq_iff_eq_mul.2 <| Eq.symm <| (hfg y).2‚ü©

@[simp]
theorem res_const (f g : R) (U hu V hv i) :
    (structureSheaf R).1.map i (const R f g U hu) = const R f g V hv :=
  rfl

theorem res_const' (f g : R) (V hv) :
    (structureSheaf R).1.map (homOfLE hv).op (const R f g (PrimeSpectrum.basicOpen g) fun _ => id) =
      const R f g V hv :=
  rfl

theorem const_zero (f : R) (U hu) : const R 0 f U hu = 0 :=
  Subtype.ext <| funext fun x => IsLocalization.mk'_eq_iff_eq_mul.2 <| by
    rw [RingHom.map_zero]
    exact (mul_eq_zero_of_left rfl ((algebraMap R (Localizations R x)) _)).symm

theorem const_self (f : R) (U hu) : const R f f U hu = 1 :=
  Subtype.ext <| funext fun _ => IsLocalization.mk'_self _ _

theorem const_one (U) : (const R 1 1 U fun _ _ => Submonoid.one_mem _) = 1 :=
  const_self R 1 U _

theorem const_add (f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ : R) (U hu‚ÇÅ hu‚ÇÇ) :
    const R f‚ÇÅ g‚ÇÅ U hu‚ÇÅ + const R f‚ÇÇ g‚ÇÇ U hu‚ÇÇ =
      const R (f‚ÇÅ * g‚ÇÇ + f‚ÇÇ * g‚ÇÅ) (g‚ÇÅ * g‚ÇÇ) U fun x hx =>
        Submonoid.mul_mem _ (hu‚ÇÅ x hx) (hu‚ÇÇ x hx) :=
  Subtype.ext <| funext fun x => Eq.symm <| IsLocalization.mk'_add _ _
    ‚ü®g‚ÇÅ, hu‚ÇÅ x x.2‚ü© ‚ü®g‚ÇÇ, hu‚ÇÇ x x.2‚ü©

theorem const_mul (f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ : R) (U hu‚ÇÅ hu‚ÇÇ) :
    const R f‚ÇÅ g‚ÇÅ U hu‚ÇÅ * const R f‚ÇÇ g‚ÇÇ U hu‚ÇÇ =
      const R (f‚ÇÅ * f‚ÇÇ) (g‚ÇÅ * g‚ÇÇ) U fun x hx => Submonoid.mul_mem _ (hu‚ÇÅ x hx) (hu‚ÇÇ x hx) :=
  Subtype.ext <|
    funext fun x =>
      Eq.symm <| IsLocalization.mk'_mul _ f‚ÇÅ f‚ÇÇ ‚ü®g‚ÇÅ, hu‚ÇÅ x x.2‚ü© ‚ü®g‚ÇÇ, hu‚ÇÇ x x.2‚ü©

theorem const_ext {f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ : R} {U hu‚ÇÅ hu‚ÇÇ} (h : f‚ÇÅ * g‚ÇÇ = f‚ÇÇ * g‚ÇÅ) :
    const R f‚ÇÅ g‚ÇÅ U hu‚ÇÅ = const R f‚ÇÇ g‚ÇÇ U hu‚ÇÇ :=
  Subtype.ext <|
    funext fun x =>
      IsLocalization.mk'_eq_of_eq (by rw [mul_comm, Subtype.coe_mk, ‚Üê h, mul_comm, Subtype.coe_mk])

theorem const_congr {f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ : R} {U hu} (hf : f‚ÇÅ = f‚ÇÇ) (hg : g‚ÇÅ = g‚ÇÇ) :
    const R f‚ÇÅ g‚ÇÅ U hu = const R f‚ÇÇ g‚ÇÇ U (hg ‚ñ∏ hu) := by substs hf hg; rfl

theorem const_mul_rev (f g : R) (U hu‚ÇÅ hu‚ÇÇ) : const R f g U hu‚ÇÅ * const R g f U hu‚ÇÇ = 1 := by
  rw [const_mul, const_congr R rfl (mul_comm g f), const_self]

theorem const_mul_cancel (f g‚ÇÅ g‚ÇÇ : R) (U hu‚ÇÅ hu‚ÇÇ) :
    const R f g‚ÇÅ U hu‚ÇÅ * const R g‚ÇÅ g‚ÇÇ U hu‚ÇÇ = const R f g‚ÇÇ U hu‚ÇÇ := by
  rw [const_mul, const_ext]; rw [mul_assoc]

theorem const_mul_cancel' (f g‚ÇÅ g‚ÇÇ : R) (U hu‚ÇÅ hu‚ÇÇ) :
    const R g‚ÇÅ g‚ÇÇ U hu‚ÇÇ * const R f g‚ÇÅ U hu‚ÇÅ = const R f g‚ÇÇ U hu‚ÇÇ := by
  rw [mul_comm, const_mul_cancel]

/-- The canonical ring homomorphism interpreting an element of `R` as
a section of the structure sheaf. -/
def toOpen (U : Opens (PrimeSpectrum.Top R)) :
    CommRingCat.of R ‚ü∂ (structureSheaf R).1.obj (op U) := CommRingCat.ofHom
  { toFun f :=
      ‚ü®fun _ => algebraMap R _ f, fun x =>
        ‚ü®U, x.2, ùüô _, f, 1, fun y =>
          ‚ü®(Ideal.ne_top_iff_one _).1 y.1.2.1, by simp [RingHom.map_one, mul_one]‚ü©‚ü©‚ü©
    map_one' := Subtype.ext <| funext fun _ => RingHom.map_one _
    map_mul' _ _ := Subtype.ext <| funext fun _ => RingHom.map_mul _ _ _
    map_zero' := Subtype.ext <| funext fun _ => RingHom.map_zero _
    map_add' _ _ := Subtype.ext <| funext fun _ => RingHom.map_add _ _ _ }

@[simp]
theorem toOpen_res (U V : Opens (PrimeSpectrum.Top R)) (i : V ‚ü∂ U) :
    toOpen R U ‚â´ (structureSheaf R).1.map i.op = toOpen R V :=
  rfl

@[simp]
theorem toOpen_apply (U : Opens (PrimeSpectrum.Top R)) (f : R) (x : U) :
    (toOpen R U f).1 x = algebraMap _ _ f :=
  rfl

theorem toOpen_eq_const (U : Opens (PrimeSpectrum.Top R)) (f : R) :
    toOpen R U f = const R f 1 U fun x _ => (Ideal.ne_top_iff_one _).1 x.2.1 :=
  Subtype.ext <| funext fun _ => Eq.symm <| IsLocalization.mk'_one _ f

/-- The canonical ring homomorphism interpreting an element of `R` as an element of
the stalk of `structureSheaf R` at `x`. -/
def toStalk (x : PrimeSpectrum.Top R) : CommRingCat.of R ‚ü∂ (structureSheaf R).presheaf.stalk x :=
  (toOpen R ‚ä§ ‚â´ (structureSheaf R).presheaf.germ _ x (by trivial))

@[simp]
theorem toOpen_germ (U : Opens (PrimeSpectrum.Top R)) (x : PrimeSpectrum.Top R) (hx : x ‚àà U) :
    toOpen R U ‚â´ (structureSheaf R).presheaf.germ U x hx = toStalk R x := by
  rw [‚Üê toOpen_res R ‚ä§ U (homOfLE le_top : U ‚ü∂ ‚ä§), Category.assoc, Presheaf.germ_res]; rfl

@[simp]
theorem germ_toOpen
    (U : Opens (PrimeSpectrum.Top R)) (x : PrimeSpectrum.Top R) (hx : x ‚àà U) (f : R) :
    (structureSheaf R).presheaf.germ U x hx (toOpen R U f) = toStalk R x f := by
  rw [‚Üê toOpen_germ]; rfl

theorem toOpen_Œìgerm_apply (x : PrimeSpectrum.Top R) (f : R) :
    (structureSheaf R).presheaf.Œìgerm x (toOpen R ‚ä§ f) = toStalk R x f :=
  rfl

theorem isUnit_to_basicOpen_self (f : R) : IsUnit (toOpen R (PrimeSpectrum.basicOpen f) f) :=
  isUnit_of_mul_eq_one _ (const R 1 f (PrimeSpectrum.basicOpen f) fun _ => id) <| by
    rw [toOpen_eq_const, const_mul_rev]

theorem isUnit_toStalk (x : PrimeSpectrum.Top R) (f : x.asIdeal.primeCompl) :
    IsUnit (toStalk R x (f : R)) := by
  rw [‚Üê germ_toOpen R (PrimeSpectrum.basicOpen (f : R)) x f.2 (f : R)]
  exact RingHom.isUnit_map _ (isUnit_to_basicOpen_self R f)

/-- The canonical ring homomorphism from the localization of `R` at `p` to the stalk
of the structure sheaf at the point `p`. -/
def localizationToStalk (x : PrimeSpectrum.Top R) :
    CommRingCat.of (Localization.AtPrime x.asIdeal) ‚ü∂ (structureSheaf R).presheaf.stalk x :=
  CommRingCat.ofHom <|
    show Localization.AtPrime x.asIdeal ‚Üí+* _ from IsLocalization.lift (isUnit_toStalk R x)

@[simp]
theorem localizationToStalk_of (x : PrimeSpectrum.Top R) (f : R) :
    localizationToStalk R x (algebraMap _ (Localization _) f) = toStalk R x f :=
  IsLocalization.lift_eq (S := Localization x.asIdeal.primeCompl) _ f

@[simp]
theorem localizationToStalk_mk' (x : PrimeSpectrum.Top R) (f : R) (s : x.asIdeal.primeCompl) :
    localizationToStalk R x (IsLocalization.mk' (Localization.AtPrime x.asIdeal) f s) =
      (structureSheaf R).presheaf.germ (PrimeSpectrum.basicOpen (s : R)) x s.2
        (const R f s (PrimeSpectrum.basicOpen s) fun _ => id) :=
  (IsLocalization.lift_mk'_spec (S := Localization.AtPrime x.asIdeal) _ _ _ _).2 <| by
    rw [‚Üê germ_toOpen R (PrimeSpectrum.basicOpen s) x s.2,
      ‚Üê germ_toOpen R (PrimeSpectrum.basicOpen s) x s.2, ‚Üê RingHom.map_mul, toOpen_eq_const,
      toOpen_eq_const, const_mul_cancel']

/-- The ring homomorphism that takes a section of the structure sheaf of `R` on the open set `U`,
implemented as a subtype of dependent functions to localizations at prime ideals, and evaluates
the section on the point corresponding to a given prime ideal. -/
def openToLocalization (U : Opens (PrimeSpectrum.Top R)) (x : PrimeSpectrum.Top R) (hx : x ‚àà U) :
    (structureSheaf R).1.obj (op U) ‚ü∂ CommRingCat.of (Localization.AtPrime x.asIdeal) :=
  CommRingCat.ofHom
  { toFun s := (s.1 ‚ü®x, hx‚ü© :)
    map_one' := rfl
    map_mul' _ _ := rfl
    map_zero' := rfl
    map_add' _ _ := rfl }

@[simp]
theorem coe_openToLocalization (U : Opens (PrimeSpectrum.Top R)) (x : PrimeSpectrum.Top R)
    (hx : x ‚àà U) :
    (openToLocalization R U x hx :
        (structureSheaf R).1.obj (op U) ‚Üí Localization.AtPrime x.asIdeal) =
      fun s => s.1 ‚ü®x, hx‚ü© :=
  rfl

theorem openToLocalization_apply (U : Opens (PrimeSpectrum.Top R)) (x : PrimeSpectrum.Top R)
    (hx : x ‚àà U) (s : (structureSheaf R).1.obj (op U)) :
    openToLocalization R U x hx s = s.1 ‚ü®x, hx‚ü© :=
  rfl

/-- The ring homomorphism from the stalk of the structure sheaf of `R` at a point corresponding to
a prime ideal `p` to the localization of `R` at `p`,
formed by gluing the `openToLocalization` maps. -/
def stalkToFiberRingHom (x : PrimeSpectrum.Top R) :
    (structureSheaf R).presheaf.stalk x ‚ü∂ CommRingCat.of (Localization.AtPrime x.asIdeal) :=
  Limits.colimit.desc ((OpenNhds.inclusion x).op ‚ãô (structureSheaf R).1)
    { pt := _
      Œπ := { app := fun U =>
        openToLocalization R ((OpenNhds.inclusion _).obj (unop U)) x (unop U).2 } }

@[simp]
theorem germ_comp_stalkToFiberRingHom
    (U : Opens (PrimeSpectrum.Top R)) (x : PrimeSpectrum.Top R) (hx : x ‚àà U) :
    (structureSheaf R).presheaf.germ U x hx ‚â´ stalkToFiberRingHom R x =
      openToLocalization R U x hx :=
  Limits.colimit.Œπ_desc _ _

@[simp]
theorem stalkToFiberRingHom_germ (U : Opens (PrimeSpectrum.Top R))
    (x : PrimeSpectrum.Top R) (hx : x ‚àà U) (s : (structureSheaf R).1.obj (op U)) :
    stalkToFiberRingHom R x ((structureSheaf R).presheaf.germ U x hx s) = s.1 ‚ü®x, hx‚ü© :=
  RingHom.ext_iff.mp (CommRingCat.hom_ext_iff.mp (germ_comp_stalkToFiberRingHom R U x hx)) s

@[simp]
theorem toStalk_comp_stalkToFiberRingHom (x : PrimeSpectrum.Top R) :
    toStalk R x ‚â´ stalkToFiberRingHom R x = CommRingCat.ofHom (algebraMap _ _) := by
  rw [toStalk, Category.assoc, germ_comp_stalkToFiberRingHom]; rfl

@[simp]
theorem stalkToFiberRingHom_toStalk (x : PrimeSpectrum.Top R) (f : R) :
    stalkToFiberRingHom R x (toStalk R x f) = algebraMap _ _ f :=
  RingHom.ext_iff.1 (CommRingCat.hom_ext_iff.mp (toStalk_comp_stalkToFiberRingHom R x)) _

/-- The ring isomorphism between the stalk of the structure sheaf of `R` at a point `p`
corresponding to a prime ideal in `R` and the localization of `R` at `p`. -/
@[simps]
def stalkIso (x : PrimeSpectrum.Top R) :
    (structureSheaf R).presheaf.stalk x ‚âÖ CommRingCat.of (Localization.AtPrime x.asIdeal) where
  hom := stalkToFiberRingHom R x
  inv := localizationToStalk R x
  hom_inv_id := by
    apply stalk_hom_ext
    intro U hxU
    ext s
    dsimp only [CommRingCat.hom_comp, RingHom.coe_comp, Function.comp_apply, CommRingCat.hom_id,
      RingHom.coe_id, id_eq]
    rw [stalkToFiberRingHom_germ]
    obtain ‚ü®V, hxV, iVU, f, g, (hg : V ‚â§ PrimeSpectrum.basicOpen _), hs‚ü© :=
      exists_const _ _ s x hxU
    have := res_apply R U V iVU s ‚ü®x, hxV‚ü©
    dsimp only [isLocallyFraction_pred, Opens.apply_mk] at this
    rw [‚Üê this, ‚Üê hs, const_apply, localizationToStalk_mk']
    refine (structureSheaf R).presheaf.germ_ext V hxV (homOfLE hg) iVU ?_
    rw [‚Üê hs, res_const']
  inv_hom_id := CommRingCat.hom_ext <| IsLocalization.ringHom_ext x.asIdeal.primeCompl <| by
    ext f
    rw [CommRingCat.hom_comp, CommRingCat.hom_id,
      RingHom.comp_apply, RingHom.comp_apply, localizationToStalk_of,
      stalkToFiberRingHom_toStalk, RingHom.comp_apply, RingHom.id_apply]

instance (x : PrimeSpectrum R) : IsIso (stalkToFiberRingHom R x) :=
  (stalkIso R x).isIso_hom

instance (x : PrimeSpectrum R) : IsLocalHom (stalkToFiberRingHom R x).hom :=
  isLocalHom_of_isIso _

instance (x : PrimeSpectrum R) : IsIso (localizationToStalk R x) :=
  (stalkIso R x).isIso_inv

instance (x : PrimeSpectrum R) : IsLocalHom (localizationToStalk R x).hom :=
  isLocalHom_of_isIso _

@[simp, reassoc]
theorem stalkToFiberRingHom_localizationToStalk (x : PrimeSpectrum.Top R) :
    stalkToFiberRingHom R x ‚â´ localizationToStalk R x = ùüô _ :=
  (stalkIso R x).hom_inv_id

@[simp, reassoc]
theorem localizationToStalk_stalkToFiberRingHom (x : PrimeSpectrum.Top R) :
    localizationToStalk R x ‚â´ stalkToFiberRingHom R x = ùüô _ :=
  (stalkIso R x).inv_hom_id

/-- The canonical ring homomorphism interpreting `s ‚àà R_f` as a section of the structure sheaf
on the basic open defined by `f ‚àà R`. -/
def toBasicOpen (f : R) :
    Localization.Away f ‚Üí+* (structureSheaf R).1.obj (op <| PrimeSpectrum.basicOpen f) :=
  IsLocalization.Away.lift f (isUnit_to_basicOpen_self R f)

@[simp]
theorem toBasicOpen_mk' (s f : R) (g : Submonoid.powers s) :
    toBasicOpen R s (IsLocalization.mk' (Localization.Away s) f g) =
      const R f g (PrimeSpectrum.basicOpen s) fun _ hx => Submonoid.powers_le.2 hx g.2 :=
  (IsLocalization.lift_mk'_spec _ _ _ _).2 <| by
    rw [toOpen_eq_const, toOpen_eq_const, const_mul_cancel']

@[simp]
theorem localization_toBasicOpen (f : R) :
    RingHom.comp (toBasicOpen R f) (algebraMap R (Localization.Away f)) =
    (toOpen R (PrimeSpectrum.basicOpen f)).hom :=
  RingHom.ext fun g => by
    rw [toBasicOpen, IsLocalization.Away.lift, RingHom.comp_apply, IsLocalization.lift_eq]

@[simp]
theorem toBasicOpen_to_map (s f : R) :
    toBasicOpen R s (algebraMap R (Localization.Away s) f) =
      const R f 1 (PrimeSpectrum.basicOpen s) fun _ _ => Submonoid.one_mem _ :=
  (IsLocalization.lift_eq _ _).trans <| toOpen_eq_const _ _ _

-- The proof here follows the argument in Hartshorne's Algebraic Geometry, Proposition II.2.2.
theorem toBasicOpen_injective (f : R) : Function.Injective (toBasicOpen R f) := by
  intro s t h_eq
  obtain ‚ü®a, ‚ü®b, hb‚ü©, rfl‚ü© := IsLocalization.exists_mk'_eq (Submonoid.powers f) s
  obtain ‚ü®c, ‚ü®d, hd‚ü©, rfl‚ü© := IsLocalization.exists_mk'_eq (Submonoid.powers f) t
  simp only [toBasicOpen_mk'] at h_eq
  rw [IsLocalization.eq]
  -- We know that the fractions `a/b` and `c/d` are equal as sections of the structure sheaf on
  -- `basicOpen f`. We need to show that they agree as elements in the localization of `R` at `f`.
  -- This amounts showing that `r * (d * a) = r * (b * c)`, for some power `r = f ^ n` of `f`.
  -- We define `I` as the ideal of *all* elements `r` satisfying the above equation.
  let I : Ideal R :=
    { carrier := { r : R | r * (d * a) = r * (b * c) }
      zero_mem' := by simp only [Set.mem_setOf_eq, zero_mul]
      add_mem' := fun {r‚ÇÅ r‚ÇÇ} hr‚ÇÅ hr‚ÇÇ => by dsimp at hr‚ÇÅ hr‚ÇÇ ‚ä¢; simp only [add_mul, hr‚ÇÅ, hr‚ÇÇ]
      smul_mem' := fun {r‚ÇÅ r‚ÇÇ} hr‚ÇÇ => by dsimp at hr‚ÇÇ ‚ä¢; simp only [mul_assoc, hr‚ÇÇ] }
  -- Our claim now reduces to showing that `f` is contained in the radical of `I`
  suffices f ‚àà I.radical by
    obtain ‚ü®n, hn‚ü© := this
    exact ‚ü®‚ü®f ^ n, n, rfl‚ü©, hn‚ü©
  rw [‚Üê PrimeSpectrum.vanishingIdeal_zeroLocus_eq_radical, PrimeSpectrum.mem_vanishingIdeal]
  intro p hfp
  contrapose hfp
  rw [PrimeSpectrum.mem_zeroLocus, Set.not_subset]
  have := congr_fun (congr_arg Subtype.val h_eq) ‚ü®p, hfp‚ü©
  dsimp at this
  rw [IsLocalization.eq (S := Localization.AtPrime p.asIdeal)] at this
  obtain ‚ü®r, hr‚ü© := this
  exact ‚ü®r.1, hr, r.2‚ü©

/-
Auxiliary lemma for surjectivity of `toBasicOpen`.
Every section can locally be represented on basic opens `basicOpen g` as a fraction `f/g`
-/
theorem locally_const_basicOpen (U : Opens (PrimeSpectrum.Top R))
    (s : (structureSheaf R).1.obj (op U)) (x : U) :
    ‚àÉ (f g : R) (i : PrimeSpectrum.basicOpen g ‚ü∂ U), x.1 ‚àà PrimeSpectrum.basicOpen g ‚àß
      (const R f g (PrimeSpectrum.basicOpen g) fun _ hy => hy) =
      (structureSheaf R).1.map i.op s := by
  -- First, any section `s` can be represented as a fraction `f/g` on some open neighborhood of `x`
  -- and we may pass to a `basicOpen h`, since these form a basis
  obtain ‚ü®V, hxV : x.1 ‚àà V.1, iVU, f, g, hVDg : V ‚â§ PrimeSpectrum.basicOpen g, s_eq‚ü© :=
    exists_const R U s x.1 x.2
  obtain ‚ü®_, ‚ü®h, rfl‚ü©, hxDh, hDhV : PrimeSpectrum.basicOpen h ‚â§ V‚ü© :=
    PrimeSpectrum.isTopologicalBasis_basic_opens.exists_subset_of_mem_open hxV V.2
  -- The problem is of course, that `g` and `h` don't need to coincide.
  -- But, since `basicOpen h ‚â§ basicOpen g`, some power of `h` must be a multiple of `g`
  obtain ‚ü®n, hn‚ü© := (PrimeSpectrum.basicOpen_le_basicOpen_iff h g).mp (Set.Subset.trans hDhV hVDg)
  -- Actually, we will need a *nonzero* power of `h`.
  -- This is because we will need the equality `basicOpen (h ^ n) = basicOpen h`, which only
  -- holds for a nonzero power `n`. We therefore artificially increase `n` by one.
  replace hn := Ideal.mul_mem_right h (Ideal.span {g}) hn
  rw [‚Üê pow_succ, Ideal.mem_span_singleton'] at hn
  obtain ‚ü®c, hc‚ü© := hn
  have basic_opens_eq := PrimeSpectrum.basicOpen_pow h (n + 1) (by cutsat)
  have i_basic_open := eqToHom basic_opens_eq ‚â´ homOfLE hDhV
  -- We claim that `(f * c) / h ^ (n + 1)` is our desired representation
  use f * c, h ^ (n + 1), i_basic_open ‚â´ iVU, (basic_opens_eq.symm.le :) hxDh
  rw [op_comp, Functor.map_comp, ConcreteCategory.comp_apply, ‚Üê s_eq, res_const]
  -- Note that the last rewrite here generated an additional goal, which was a parameter
  -- of `res_const`. We prove this goal first
  swap
  ¬∑ intro y hy
    rw [basic_opens_eq] at hy
    exact (Set.Subset.trans hDhV hVDg :) hy
  -- All that is left is a simple calculation
  apply const_ext
  rw [mul_assoc f c g, hc]

/-
Auxiliary lemma for surjectivity of `toBasicOpen`.
A local representation of a section `s` as fractions `a i / h i` on finitely many basic opens
`basicOpen (h i)` can be "normalized" in such a way that `a i * h j = h i * a j` for all `i, j`
-/
theorem normalize_finite_fraction_representation (U : Opens (PrimeSpectrum.Top R))
    (s : (structureSheaf R).1.obj (op U)) {Œπ : Type*} (t : Finset Œπ) (a h : Œπ ‚Üí R)
    (iDh : ‚àÄ i : Œπ, PrimeSpectrum.basicOpen (h i) ‚ü∂ U)
    (h_cover : U ‚â§ ‚®Ü i ‚àà t, PrimeSpectrum.basicOpen (h i))
    (hs :
      ‚àÄ i : Œπ,
        (const R (a i) (h i) (PrimeSpectrum.basicOpen (h i)) fun _ hy => hy) =
          (structureSheaf R).1.map (iDh i).op s) :
    ‚àÉ (a' h' : Œπ ‚Üí R) (iDh' : ‚àÄ i : Œπ, PrimeSpectrum.basicOpen (h' i) ‚ü∂ U),
      (U ‚â§ ‚®Ü i ‚àà t, PrimeSpectrum.basicOpen (h' i)) ‚àß
        (‚àÄ (i) (_ : i ‚àà t) (j) (_ : j ‚àà t), a' i * h' j = h' i * a' j) ‚àß
          ‚àÄ i ‚àà t,
            (structureSheaf R).1.map (iDh' i).op s =
              const R (a' i) (h' i) (PrimeSpectrum.basicOpen (h' i)) fun _ hy => hy := by
  -- First we show that the fractions `(a i * h j) / (h i * h j)` and `(h i * a j) / (h i * h j)`
  -- coincide in the localization of `R` at `h i * h j`
  have fractions_eq :
    ‚àÄ i j : Œπ,
      IsLocalization.mk' (Localization.Away (h i * h j))
        (a i * h j) ‚ü®h i * h j, Submonoid.mem_powers _‚ü© =
      IsLocalization.mk' _ (h i * a j) ‚ü®h i * h j, Submonoid.mem_powers _‚ü© := by
    intro i j
    let D := PrimeSpectrum.basicOpen (h i * h j)
    let iDi : D ‚ü∂ PrimeSpectrum.basicOpen (h i) := homOfLE (PrimeSpectrum.basicOpen_mul_le_left _ _)
    let iDj : D ‚ü∂ PrimeSpectrum.basicOpen (h j) :=
      homOfLE (PrimeSpectrum.basicOpen_mul_le_right _ _)
    -- Crucially, we need injectivity of `toBasicOpen`
    apply toBasicOpen_injective R (h i * h j)
    rw [toBasicOpen_mk', toBasicOpen_mk']
    simp only []
    -- Here, both sides of the equation are equal to a restriction of `s`
    trans
    on_goal 1 =>
      convert congr_arg ((structureSheaf R).1.map iDj.op) (hs j).symm using 1
      convert congr_arg ((structureSheaf R).1.map iDi.op) (hs i) using 1
    all_goals rw [res_const]; apply const_ext; ring
    -- The remaining two goals were generated during the rewrite of `res_const`
    -- These can be solved immediately
    exacts [PrimeSpectrum.basicOpen_mul_le_left _ _, PrimeSpectrum.basicOpen_mul_le_right _ _]
  -- From the equality in the localization, we obtain for each `(i,j)` some power `(h i * h j) ^ n`
  -- which equalizes `a i * h j` and `h i * a j`
  have exists_power :
    ‚àÄ i j : Œπ, ‚àÉ n : ‚Ñï, a i * h j * (h i * h j) ^ n = h i * a j * (h i * h j) ^ n := by
    intro i j
    obtain ‚ü®‚ü®c, n, rfl‚ü©, hc‚ü© := IsLocalization.eq.mp (fractions_eq i j)
    use n + 1
    rw [pow_succ]
    dsimp at hc
    convert hc using 1 <;> ring
  let n := fun p : Œπ √ó Œπ => (exists_power p.1 p.2).choose
  have n_spec := fun p : Œπ √ó Œπ => (exists_power p.fst p.snd).choose_spec
  -- We need one power `(h i * h j) ^ N` that works for *all* pairs `(i,j)`
  -- Since there are only finitely many indices involved, we can pick the supremum.
  let N := (t √óÀ¢ t).sup n
  have basic_opens_eq : ‚àÄ i : Œπ, PrimeSpectrum.basicOpen (h i ^ (N + 1)) =
    PrimeSpectrum.basicOpen (h i) := fun i => PrimeSpectrum.basicOpen_pow _ _ (by cutsat)
  -- Expanding the fraction `a i / h i` by the power `(h i) ^ n` gives the desired normalization
  refine
    ‚ü®fun i => a i * h i ^ N, fun i => h i ^ (N + 1), fun i => eqToHom (basic_opens_eq i) ‚â´ iDh i,
      ?_, ?_, ?_‚ü©
  ¬∑ simpa only [basic_opens_eq] using h_cover
  ¬∑ intro i hi j hj
    -- Here we need to show that our new fractions `a i / h i` satisfy the normalization condition
    -- Of course, the power `N` we used to expand the fractions might be bigger than the power
    -- `n (i, j)` which was originally chosen. We denote their difference by `k`
    have n_le_N : n (i, j) ‚â§ N := Finset.le_sup (Finset.mem_product.mpr ‚ü®hi, hj‚ü©)
    obtain ‚ü®k, hk‚ü© := Nat.le.dest n_le_N
    simp only [‚Üê hk, pow_add, pow_one]
    -- To accommodate for the difference `k`, we multiply both sides of the equation `n_spec (i, j)`
    -- by `(h i * h j) ^ k`
    convert congr_arg (fun z => z * (h i * h j) ^ k) (n_spec (i, j)) using 1 <;>
      ¬∑ simp only [n, mul_pow]; ring
  -- Lastly, we need to show that the new fractions still represent our original `s`
  intro i _
  rw [op_comp, Functor.map_comp, ConcreteCategory.comp_apply, ‚Üê hs, res_const]
  -- additional goal spit out by `res_const`
  swap
  ¬∑ exact (basic_opens_eq i).le
  apply const_ext
  dsimp
  rw [pow_succ]
  ring

-- The proof here follows the argument in Hartshorne's Algebraic Geometry, Proposition II.2.2.
theorem toBasicOpen_surjective (f : R) : Function.Surjective (toBasicOpen R f) := by
  intro s
  -- In this proof, `basicOpen f` will play two distinct roles: Firstly, it is an open set in the
  -- prime spectrum. Secondly, it is used as an indexing type for various families of objects
  -- (open sets, ring elements, ...). In order to make the distinction clear, we introduce a type
  -- alias `Œπ` that is used whenever we want think of it as an indexing type.
  let Œπ : Type u := PrimeSpectrum.basicOpen f
  -- First, we pick some cover of basic opens, on which we can represent `s` as a fraction
  choose a' h' iDh' hxDh' s_eq' using locally_const_basicOpen R (PrimeSpectrum.basicOpen f) s
  -- Since basic opens are compact, we can pass to a finite subcover
  obtain ‚ü®t, ht_cover'‚ü© :=
    (PrimeSpectrum.isCompact_basicOpen f).elim_finite_subcover
      (fun i : Œπ => PrimeSpectrum.basicOpen (h' i)) (fun i => PrimeSpectrum.isOpen_basicOpen)
      -- Here, we need to show that our basic opens actually form a cover of `basicOpen f`
      fun x hx => by rw [Set.mem_iUnion]; exact ‚ü®‚ü®x, hx‚ü©, hxDh' ‚ü®x, hx‚ü©‚ü©
  simp only [‚Üê Opens.coe_iSup, SetLike.coe_subset_coe] at ht_cover'
  -- We use the normalization lemma from above to obtain the relation `a i * h j = h i * a j`
  obtain ‚ü®a, h, iDh, ht_cover, ah_ha, s_eq‚ü© :=
    normalize_finite_fraction_representation R (PrimeSpectrum.basicOpen f)
      s t a' h' iDh' ht_cover' s_eq'
  clear s_eq' iDh' hxDh' ht_cover' a' h'
  simp only [‚Üê SetLike.coe_subset_coe, Opens.coe_iSup] at ht_cover
  replace ht_cover : (PrimeSpectrum.basicOpen f : Set <| PrimeSpectrum R) ‚äÜ
      ‚ãÉ (i : Œπ) (x : i ‚àà t), (PrimeSpectrum.basicOpen (h i) : Set _) := ht_cover
  -- Next we show that some power of `f` is a linear combination of the `h i`
  obtain ‚ü®n, hn‚ü© : f ‚àà (Ideal.span (h '' ‚Üët)).radical := by
    rw [‚Üê PrimeSpectrum.vanishingIdeal_zeroLocus_eq_radical, PrimeSpectrum.zeroLocus_span]
    simp only [PrimeSpectrum.basicOpen_eq_zeroLocus_compl] at ht_cover
    replace ht_cover : (PrimeSpectrum.zeroLocus {f})·∂ú ‚äÜ
        ‚ãÉ (i : Œπ) (x : i ‚àà t), (PrimeSpectrum.zeroLocus {h i})·∂ú := ht_cover
    rw [Set.compl_subset_comm] at ht_cover
    -- Why doesn't `simp_rw` do this?
    simp_rw [Set.compl_iUnion, compl_compl, ‚Üê PrimeSpectrum.zeroLocus_iUnion,
      ‚Üê Finset.set_biUnion_coe, ‚Üê Set.image_eq_iUnion] at ht_cover
    apply PrimeSpectrum.vanishingIdeal_anti_mono ht_cover
    exact PrimeSpectrum.subset_vanishingIdeal_zeroLocus {f} (Set.mem_singleton f)
  replace hn := Ideal.mul_mem_right f _ hn
  rw [‚Üê pow_succ, Ideal.span, Finsupp.mem_span_image_iff_linearCombination] at hn
  rcases hn with ‚ü®b, b_supp, hb‚ü©
  rw [Finsupp.linearCombination_apply_of_mem_supported R b_supp] at hb
  dsimp at hb
  -- Finally, we have all the ingredients.
  -- We claim that our preimage is given by `(‚àë (i : Œπ) ‚àà t, b i * a i) / f ^ (n + 1)`
  use
    IsLocalization.mk' (Localization.Away f) (‚àë i ‚àà t, b i * a i)
      (‚ü®f ^ (n + 1), n + 1, rfl‚ü© : Submonoid.powers _)
  rw [toBasicOpen_mk']
  -- Since the structure sheaf is a sheaf, we can show the desired equality locally.
  -- Annoyingly, `Sheaf.eq_of_locally_eq'` requires an open cover indexed by a *type*, so we need to
  -- coerce our finset `t` to a type first.
  let tt := ((t : Set (PrimeSpectrum.basicOpen f)) : Type u)
  apply
    (structureSheaf R).eq_of_locally_eq' (fun i : tt => PrimeSpectrum.basicOpen (h i))
      (PrimeSpectrum.basicOpen f) fun i : tt => iDh i
  ¬∑ -- This feels a little redundant, since already have `ht_cover` as a hypothesis
    -- Unfortunately, `ht_cover` uses a bounded union over the set `t`, while here we have the
    -- Union indexed by the type `tt`, so we need some boilerplate to translate one to the other
    intro x hx
    rw [SetLike.mem_coe, TopologicalSpace.Opens.mem_iSup]
    have := ht_cover hx
    rw [‚Üê Finset.set_biUnion_coe, Set.mem_iUnion‚ÇÇ] at this
    rcases this with ‚ü®i, i_mem, x_mem‚ü©
    exact ‚ü®‚ü®i, i_mem‚ü©, x_mem‚ü©
  rintro ‚ü®i, hi‚ü©
  dsimp
  change (structureSheaf R).1.map (iDh i).op _ = (structureSheaf R).1.map (iDh i).op _
  rw [s_eq i hi, res_const]
  -- Again, `res_const` spits out an additional goal
  swap
  ¬∑ intro y hy
    change y ‚àà PrimeSpectrum.basicOpen (f ^ (n + 1))
    rw [PrimeSpectrum.basicOpen_pow f (n + 1) (by cutsat)]
    exact (leOfHom (iDh i) :) hy
  -- The rest of the proof is just computation
  apply const_ext
  rw [‚Üê hb, Finset.sum_mul, Finset.mul_sum]
  apply Finset.sum_congr rfl
  intro j hj
  rw [mul_assoc, ah_ha j hj i hi]
  ring

instance isIso_toBasicOpen (f : R) :
    IsIso (CommRingCat.ofHom (toBasicOpen R f)) :=
  haveI : IsIso ((forget CommRingCat).map (CommRingCat.ofHom (toBasicOpen R f))) :=
    (isIso_iff_bijective _).mpr ‚ü®toBasicOpen_injective R f, toBasicOpen_surjective R f‚ü©
  isIso_of_reflects_iso _ (forget CommRingCat)

/-- The ring isomorphism between the structure sheaf on `basicOpen f` and the localization of `R`
at the submonoid of powers of `f`. -/
def basicOpenIso (f : R) :
    (structureSheaf R).1.obj (op (PrimeSpectrum.basicOpen f)) ‚âÖ
    CommRingCat.of (Localization.Away f) :=
  (asIso (CommRingCat.ofHom (toBasicOpen R f))).symm

instance stalkAlgebra (p : PrimeSpectrum R) : Algebra R ((structureSheaf R).presheaf.stalk p) :=
  (toStalk R p).hom.toAlgebra

@[simp]
theorem stalkAlgebra_map (p : PrimeSpectrum R) (r : R) :
    algebraMap R ((structureSheaf R).presheaf.stalk p) r = toStalk R p r :=
  rfl

/-- Stalk of the structure sheaf at a prime p as localization of R -/
instance IsLocalization.to_stalk (p : PrimeSpectrum R) :
    IsLocalization.AtPrime ((structureSheaf R).presheaf.stalk p) p.asIdeal := by
  convert (IsLocalization.isLocalization_iff_of_ringEquiv (S := Localization.AtPrime p.asIdeal) _
          (stalkIso R p).symm.commRingCatIsoToRingEquiv).mp
      Localization.isLocalization
  apply Algebra.algebra_ext
  intro
  rw [stalkAlgebra_map]
  congr 2
  change toStalk R p = _ ‚â´ (stalkIso R p).inv
  rw [Iso.eq_comp_inv]
  exact toStalk_comp_stalkToFiberRingHom R p

instance openAlgebra (U : (Opens (PrimeSpectrum R))·µí·µñ) : Algebra R ((structureSheaf R).val.obj U) :=
  (toOpen R (unop U)).hom.toAlgebra

@[simp]
theorem openAlgebra_map (U : (Opens (PrimeSpectrum R))·µí·µñ) (r : R) :
    algebraMap R ((structureSheaf R).val.obj U) r = toOpen R (unop U) r :=
  rfl

/-- Sections of the structure sheaf of Spec R on a basic open as localization of R -/
instance IsLocalization.to_basicOpen (r : R) :
    IsLocalization.Away r ((structureSheaf R).val.obj (op <| PrimeSpectrum.basicOpen r)) := by
  convert (IsLocalization.isLocalization_iff_of_ringEquiv (S := Localization.Away r) _
      (basicOpenIso R r).symm.commRingCatIsoToRingEquiv).mp
      Localization.isLocalization
  apply Algebra.algebra_ext
  intro x
  congr 1
  exact (localization_toBasicOpen R r).symm

instance to_basicOpen_epi (r : R) : Epi (toOpen R (PrimeSpectrum.basicOpen r)) :=
  ‚ü®fun _ _ h => CommRingCat.hom_ext (IsLocalization.ringHom_ext (Submonoid.powers r)
    (CommRingCat.hom_ext_iff.mp h))‚ü©

@[elementwise]
theorem to_global_factors :
    toOpen R ‚ä§ =
      CommRingCat.ofHom (algebraMap R (Localization.Away (1 : R))) ‚â´
        CommRingCat.ofHom (toBasicOpen R (1 : R)) ‚â´
        (structureSheaf R).1.map (eqToHom PrimeSpectrum.basicOpen_one.symm).op := by
  rw [‚Üê Category.assoc]
  change toOpen R ‚ä§ =
    (CommRingCat.ofHom <| (toBasicOpen R 1).comp (algebraMap R (Localization.Away 1))) ‚â´
    (structureSheaf R).1.map (eqToHom _).op
  rw [localization_toBasicOpen R, CommRingCat.ofHom_hom, toOpen_res]

instance isIso_to_global : IsIso (toOpen R ‚ä§) := by
  let hom := CommRingCat.ofHom (algebraMap R (Localization.Away (1 : R)))
  haveI : IsIso hom :=
    (IsLocalization.atOne R (Localization.Away (1 : R))).toRingEquiv.toCommRingCatIso.isIso_hom
  rw [to_global_factors R]
  infer_instance

/-- The ring isomorphism between the ring `R` and the global sections `Œì(X, ùí™‚Çì)`. -/
@[simps! inv]
def globalSectionsIso : CommRingCat.of R ‚âÖ (structureSheaf R).1.obj (op ‚ä§) :=
  asIso (toOpen R ‚ä§)

@[simp]
theorem globalSectionsIso_hom (R : CommRingCat) : (globalSectionsIso R).hom = toOpen R ‚ä§ :=
  rfl

@[simp, reassoc, elementwise nosimp]
theorem toStalk_stalkSpecializes {R : Type*} [CommRing R] {x y : PrimeSpectrum R} (h : x ‚§≥ y) :
    toStalk R y ‚â´ (structureSheaf R).presheaf.stalkSpecializes h = toStalk R x := by
  dsimp [toStalk]; simp [-toOpen_germ]

@[simp, reassoc, elementwise nosimp]
theorem localizationToStalk_stalkSpecializes {R : Type*} [CommRing R] {x y : PrimeSpectrum R}
    (h : x ‚§≥ y) :
    StructureSheaf.localizationToStalk R y ‚â´ (structureSheaf R).presheaf.stalkSpecializes h =
      CommRingCat.ofHom (PrimeSpectrum.localizationMapOfSpecializes h) ‚â´
        StructureSheaf.localizationToStalk R x := by
  ext : 1
  apply IsLocalization.ringHom_ext (S := Localization.AtPrime y.asIdeal) y.asIdeal.primeCompl
  rw [CommRingCat.hom_comp, RingHom.comp_assoc, CommRingCat.hom_comp, RingHom.comp_assoc]
  dsimp [localizationToStalk, PrimeSpectrum.localizationMapOfSpecializes]
  rw [IsLocalization.lift_comp, IsLocalization.lift_comp, IsLocalization.lift_comp]
  exact CommRingCat.hom_ext_iff.mp (toStalk_stalkSpecializes h)

@[simp, reassoc, elementwise nosimp]
theorem stalkSpecializes_stalk_to_fiber {R : Type*} [CommRing R] {x y : PrimeSpectrum R}
    (h : x ‚§≥ y) :
    (structureSheaf R).presheaf.stalkSpecializes h ‚â´ StructureSheaf.stalkToFiberRingHom R x =
      StructureSheaf.stalkToFiberRingHom R y ‚â´
      (CommRingCat.ofHom (PrimeSpectrum.localizationMapOfSpecializes h)) := by
  change _ ‚â´ (StructureSheaf.stalkIso R x).hom = (StructureSheaf.stalkIso R y).hom ‚â´ _
  rw [‚Üê Iso.eq_comp_inv, Category.assoc, ‚Üê Iso.inv_comp_eq]
  exact localizationToStalk_stalkSpecializes h

section Comap

variable {R} {S : Type u} [CommRing S] {P : Type u} [CommRing P]

/--
Given a ring homomorphism `f : R ‚Üí+* S`, an open set `U` of the prime spectrum of `R` and an open
set `V` of the prime spectrum of `S`, such that `V ‚äÜ (comap f) ‚Åª¬π' U`, we can push a section `s`
on `U` to a section on `V`, by composing with `Localization.localRingHom _ _ f` from the left and
`comap f` from the right. Explicitly, if `s` evaluates on `comap f p` to `a / b`, its image on `V`
evaluates on `p` to `f(a) / f(b)`.

At the moment, we work with arbitrary dependent functions `s : Œ† x : U, Localizations R x`. Below,
we prove the predicate `isLocallyFraction` is preserved by this map, hence it can be extended to
a morphism between the structure sheaves of `R` and `S`.
-/
def comapFun (f : R ‚Üí+* S) (U : Opens (PrimeSpectrum.Top R)) (V : Opens (PrimeSpectrum.Top S))
    (hUV : V.1 ‚äÜ PrimeSpectrum.comap f ‚Åª¬π' U.1) (s : ‚àÄ x : U, Localizations R x) (y : V) :
    Localizations S y :=
  Localization.localRingHom (PrimeSpectrum.comap f y.1).asIdeal _ f rfl
    (s ‚ü®PrimeSpectrum.comap f y.1, hUV y.2‚ü© :)

theorem comapFunIsLocallyFraction (f : R ‚Üí+* S) (U : Opens (PrimeSpectrum.Top R))
    (V : Opens (PrimeSpectrum.Top S)) (hUV : V.1 ‚äÜ PrimeSpectrum.comap f ‚Åª¬π' U.1)
    (s : ‚àÄ x : U, Localizations R x) (hs : (isLocallyFraction R).toPrelocalPredicate.pred s) :
    (isLocallyFraction S).toPrelocalPredicate.pred (comapFun f U V hUV s) := by
  rintro ‚ü®p, hpV‚ü©
  -- Since `s` is locally fraction, we can find a neighborhood `W` of `PrimeSpectrum.comap f p`
  -- in `U`, such that `s = a / b` on `W`, for some ring elements `a, b : R`.
  rcases hs ‚ü®PrimeSpectrum.comap f p, hUV hpV‚ü© with ‚ü®W, m, iWU, a, b, h_frac‚ü©
  -- We claim that we can write our new section as the fraction `f a / f b` on the neighborhood
  -- `(comap f) ‚Åª¬π W ‚äì V` of `p`.
  refine ‚ü®Opens.comap (PrimeSpectrum.comap f) W ‚äì V, ‚ü®m, hpV‚ü©, Opens.infLERight _ _, f a, f b, ?_‚ü©
  rintro ‚ü®q, ‚ü®hqW, hqV‚ü©‚ü©
  specialize h_frac ‚ü®PrimeSpectrum.comap f q, hqW‚ü©
  refine ‚ü®h_frac.1, ?_‚ü©
  dsimp only [comapFun]
  erw [‚Üê Localization.localRingHom_to_map (PrimeSpectrum.comap f q).asIdeal _ _ rfl,
    ‚Üê RingHom.map_mul, h_frac.2, Localization.localRingHom_to_map]
  rfl

/-- For a ring homomorphism `f : R ‚Üí+* S` and open sets `U` and `V` of the prime spectra of `R` and
`S` such that `V ‚äÜ (comap f) ‚Åª¬π U`, the induced ring homomorphism from the structure sheaf of `R`
at `U` to the structure sheaf of `S` at `V`.

Explicitly, this map is given as follows: For a point `p : V`, if the section `s` evaluates on `p`
to the fraction `a / b`, its image on `V` evaluates on `p` to the fraction `f(a) / f(b)`.
-/
def comap (f : R ‚Üí+* S) (U : Opens (PrimeSpectrum.Top R)) (V : Opens (PrimeSpectrum.Top S))
    (hUV : V.1 ‚äÜ PrimeSpectrum.comap f ‚Åª¬π' U.1) :
    (structureSheaf R).1.obj (op U) ‚Üí+* (structureSheaf S).1.obj (op V) where
  toFun s := ‚ü®comapFun f U V hUV s.1, comapFunIsLocallyFraction f U V hUV s.1 s.2‚ü©
  map_one' :=
    Subtype.ext <|
      funext fun p => by
        dsimp
        rw [comapFun, (sectionsSubring R (op U)).coe_one, Pi.one_apply, RingHom.map_one]
        rfl
  map_zero' :=
    Subtype.ext <|
      funext fun p => by
        dsimp
        rw [comapFun, (sectionsSubring R (op U)).coe_zero, Pi.zero_apply, RingHom.map_zero]
        rfl
  map_add' s t :=
    Subtype.ext <|
      funext fun p => by
        dsimp
        rw [comapFun, (sectionsSubring R (op U)).coe_add, Pi.add_apply, RingHom.map_add]
        rfl
  map_mul' s t :=
    Subtype.ext <|
      funext fun p => by
        dsimp
        rw [comapFun, (sectionsSubring R (op U)).coe_mul, Pi.mul_apply, RingHom.map_mul]
        rfl

@[simp]
theorem comap_apply (f : R ‚Üí+* S) (U : Opens (PrimeSpectrum.Top R))
    (V : Opens (PrimeSpectrum.Top S)) (hUV : V.1 ‚äÜ PrimeSpectrum.comap f ‚Åª¬π' U.1)
    (s : (structureSheaf R).1.obj (op U)) (p : V) :
    (comap f U V hUV s).1 p =
      Localization.localRingHom (PrimeSpectrum.comap f p.1).asIdeal _ f rfl
        (s.1 ‚ü®PrimeSpectrum.comap f p.1, hUV p.2‚ü© :) :=
  rfl

theorem comap_const (f : R ‚Üí+* S) (U : Opens (PrimeSpectrum.Top R))
    (V : Opens (PrimeSpectrum.Top S)) (hUV : V.1 ‚äÜ PrimeSpectrum.comap f ‚Åª¬π' U.1) (a b : R)
    (hb : ‚àÄ x : PrimeSpectrum R, x ‚àà U ‚Üí b ‚àà x.asIdeal.primeCompl) :
    comap f U V hUV (const R a b U hb) =
      const S (f a) (f b) V fun p hpV => hb (PrimeSpectrum.comap f p) (hUV hpV) :=
  Subtype.ext <|
    funext fun p => by
      rw [comap_apply, const_apply, const_apply, Localization.localRingHom_mk']

/-- For an inclusion `i : V ‚ü∂ U` between open sets of the prime spectrum of `R`, the comap of the
identity from OO_X(U) to OO_X(V) equals as the restriction map of the structure sheaf.

This is a generalization of the fact that, for fixed `U`, the comap of the identity from OO_X(U)
to OO_X(U) is the identity.
-/
theorem comap_id_eq_map (U V : Opens (PrimeSpectrum.Top R)) (iVU : V ‚ü∂ U) :
    (comap (RingHom.id R) U V fun _ hpV => leOfHom iVU <| hpV) =
      ((structureSheaf R).1.map iVU.op).hom :=
  RingHom.ext fun s => Subtype.ext <| funext fun p => by
    rw [comap_apply]
    -- Unfortunately, we cannot use `Localization.localRingHom_id` here, because
    -- `PrimeSpectrum.comap (RingHom.id R) p` is not *definitionally* equal to `p`. Instead, we use
    -- that we can write `s` as a fraction `a/b` in a small neighborhood around `p`. Since
    -- `PrimeSpectrum.comap (RingHom.id R) p` equals `p`, it is also contained in the same
    -- neighborhood, hence `s` equals `a/b` there too.
    obtain ‚ü®W, hpW, iWU, h‚ü© := s.2 (iVU p)
    obtain ‚ü®a, b, h'‚ü© := h.eq_mk'
    obtain ‚ü®hb‚ÇÅ, s_eq‚ÇÅ‚ü© := h' ‚ü®p, hpW‚ü©
    obtain ‚ü®hb‚ÇÇ, s_eq‚ÇÇ‚ü© :=
      h' ‚ü®PrimeSpectrum.comap (RingHom.id _) p.1, hpW‚ü©
    dsimp only at s_eq‚ÇÅ s_eq‚ÇÇ
    erw [s_eq‚ÇÇ, Localization.localRingHom_mk', ‚Üê s_eq‚ÇÅ, ‚Üê res_apply _ _ _ iVU]

/--
The comap of the identity is the identity. In this variant of the lemma, two open subsets `U` and
`V` are given as arguments, together with a proof that `U = V`. This is useful when `U` and `V`
are not definitionally equal.
-/
theorem comap_id {U V : Opens (PrimeSpectrum.Top R)} (hUV : U = V) :
    (comap (RingHom.id R) U V fun p hpV => by rwa [hUV, PrimeSpectrum.comap_id]) =
      (eqToHom (show (structureSheaf R).1.obj (op U) = _ by rw [hUV])).hom := by
  rw [comap_id_eq_map U V (eqToHom hUV.symm), eqToHom_op, eqToHom_map]

@[simp]
theorem comap_id' (U : Opens (PrimeSpectrum.Top R)) :
    (comap (RingHom.id R) U U fun p hpU => by rwa [PrimeSpectrum.comap_id]) = RingHom.id _ := by
  rw [comap_id rfl]; rfl

theorem comap_comp (f : R ‚Üí+* S) (g : S ‚Üí+* P) (U : Opens (PrimeSpectrum.Top R))
    (V : Opens (PrimeSpectrum.Top S)) (W : Opens (PrimeSpectrum.Top P))
    (hUV : ‚àÄ p ‚àà V, PrimeSpectrum.comap f p ‚àà U) (hVW : ‚àÄ p ‚àà W, PrimeSpectrum.comap g p ‚àà V) :
    (comap (g.comp f) U W fun p hpW => hUV (PrimeSpectrum.comap g p) (hVW p hpW)) =
      (comap g V W hVW).comp (comap f U V hUV) :=
  RingHom.ext fun s =>
    Subtype.ext <|
      funext fun p => by
        rw [comap_apply, Localization.localRingHom_comp _ (PrimeSpectrum.comap g p.1).asIdeal] <;>
        simp

@[elementwise, reassoc]
theorem toOpen_comp_comap (f : R ‚Üí+* S) (U : Opens (PrimeSpectrum.Top R)) :
    (toOpen R U ‚â´ CommRingCat.ofHom (comap f U (Opens.comap (PrimeSpectrum.comap f) U)
        fun _ => id)) =
      CommRingCat.ofHom f ‚â´ toOpen S _ :=
  CommRingCat.hom_ext <| RingHom.ext fun _ => Subtype.ext <| funext fun _ =>
    Localization.localRingHom_to_map _ _ _ _ _

lemma comap_basicOpen (f : R ‚Üí+* S) (x : R) :
    comap f (PrimeSpectrum.basicOpen x) (PrimeSpectrum.basicOpen (f x))
        (PrimeSpectrum.comap_basicOpen f x).le =
      IsLocalization.map (M := .powers x) (T := .powers (f x)) _ f
        (Submonoid.powers_le.mpr (Submonoid.mem_powers _)) :=
  IsLocalization.ringHom_ext (.powers x) <| by
    simpa [CommRingCat.hom_ext_iff] using toOpen_comp_comap f _

end Comap

end StructureSheaf

end AlgebraicGeometry
