/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Geometry.RingedSpace.OpenImmersion
import Mathlib.AlgebraicGeometry.Scheme
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq
import Mathlib.CategoryTheory.MorphismProperty.Limits

/-!
# Open immersions of schemes

-/

-- Explicit universe annotations were used in this file to improve performance https://github.com/leanprover-community/mathlib4/issues/12737


noncomputable section

open TopologicalSpace CategoryTheory Opposite Topology

open CategoryTheory.Limits

namespace AlgebraicGeometry

universe v v‚ÇÅ v‚ÇÇ u

variable {C : Type u} [Category.{v} C]

/-- A morphism of Schemes is an open immersion if it is an open immersion as a morphism
of LocallyRingedSpaces
-/
abbrev IsOpenImmersion : MorphismProperty (Scheme.{u}) :=
  fun _ _ f ‚Ü¶ LocallyRingedSpace.IsOpenImmersion f.toLRSHom

instance IsOpenImmersion.comp {X Y Z : Scheme.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)
    [IsOpenImmersion f] [IsOpenImmersion g] : IsOpenImmersion (f ‚â´ g) :=
  LocallyRingedSpace.IsOpenImmersion.comp f.toLRSHom g.toLRSHom

namespace LocallyRingedSpace.IsOpenImmersion

/-- To show that a locally ringed space is a scheme, it suffices to show that it has a jointly
surjective family of open immersions from affine schemes. -/
protected def scheme (X : LocallyRingedSpace.{u})
    (h :
      ‚àÄ x : X,
        ‚àÉ (R : CommRingCat) (f : Spec.toLocallyRingedSpace.obj (op R) ‚ü∂ X),
          (x ‚àà Set.range f.base :) ‚àß LocallyRingedSpace.IsOpenImmersion f) :
    Scheme where
  toLocallyRingedSpace := X
  local_affine := by
    intro x
    obtain ‚ü®R, f, h‚ÇÅ, h‚ÇÇ‚ü© := h x
    refine ‚ü®‚ü®‚ü®_, h‚ÇÇ.base_open.isOpen_range‚ü©, h‚ÇÅ‚ü©, R, ‚ü®?_‚ü©‚ü©
    apply LocallyRingedSpace.isoOfSheafedSpaceIso
    refine SheafedSpace.forgetToPresheafedSpace.preimageIso ?_
    apply PresheafedSpace.IsOpenImmersion.isoOfRangeEq (PresheafedSpace.ofRestrict _ _) f.1
    ¬∑ exact Subtype.range_coe_subtype
    ¬∑ exact Opens.isOpenEmbedding _

end LocallyRingedSpace.IsOpenImmersion

theorem IsOpenImmersion.isOpen_range {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [H : IsOpenImmersion f] :
    IsOpen (Set.range f.base) :=
  H.base_open.isOpen_range

namespace Scheme.Hom

variable {X Y : Scheme.{u}} (f : Scheme.Hom X Y) [H : IsOpenImmersion f]

theorem isOpenEmbedding : IsOpenEmbedding f.base :=
  H.base_open

/-- The image of an open immersion as an open set. -/
@[simps]
def opensRange : Y.Opens :=
  ‚ü®_, f.isOpenEmbedding.isOpen_range‚ü©

/-- The functor `opens X ‚•§ opens Y` associated with an open immersion `f : X ‚ü∂ Y`. -/
abbrev opensFunctor : X.Opens ‚•§ Y.Opens :=
  LocallyRingedSpace.IsOpenImmersion.opensFunctor f.toLRSHom

/-- `f ''·µÅ U` is notation for the image (as an open set) of `U` under an open immersion `f`. -/
scoped[AlgebraicGeometry] notation3:90 f:91 " ''·µÅ " U:90 => (Scheme.Hom.opensFunctor f).obj U

lemma image_le_image_of_le {U V : X.Opens} (e : U ‚â§ V) : f ''·µÅ U ‚â§ f ''·µÅ V := by
  rintro a ‚ü®u, hu, rfl‚ü©
  exact Set.mem_image_of_mem (‚áëf.base) (e hu)

@[simp]
lemma opensFunctor_map_homOfLE {U V : X.Opens} (e : U ‚â§ V) :
    (Scheme.Hom.opensFunctor f).map (homOfLE e) = homOfLE (f.image_le_image_of_le e) :=
  rfl

@[simp]
lemma image_top_eq_opensRange : f ''·µÅ ‚ä§ = f.opensRange := by
  apply Opens.ext
  simp

lemma opensRange_comp {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)
    [IsOpenImmersion f] [IsOpenImmersion g] : (f ‚â´ g).opensRange = g ''·µÅ f.opensRange :=
  TopologicalSpace.Opens.ext (Set.range_comp g.base f.base)

lemma opensRange_of_isIso {X Y : Scheme} (f : X ‚ü∂ Y) [IsIso f] :
    f.opensRange = ‚ä§ :=
  TopologicalSpace.Opens.ext (Set.range_eq_univ.mpr f.homeomorph.surjective)

lemma opensRange_comp_of_isIso {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)
    [IsIso f] [IsOpenImmersion g] : (f ‚â´ g).opensRange = g.opensRange := by
  rw [opensRange_comp, opensRange_of_isIso, image_top_eq_opensRange]

@[simp]
lemma preimage_image_eq (U : X.Opens) : f ‚Åª¬π·µÅ f ''·µÅ U = U := by
  apply Opens.ext
  simp [Set.preimage_image_eq _ f.isOpenEmbedding.injective]

lemma image_le_image_iff (f : X ‚ü∂ Y) [IsOpenImmersion f] (U U' : X.Opens) :
    f ''·µÅ U ‚â§ f ''·µÅ U' ‚Üî U ‚â§ U' := by
  refine ‚ü®fun h ‚Ü¶ ?_, image_le_image_of_le f‚ü©
  rw [‚Üê preimage_image_eq f U, ‚Üê preimage_image_eq f U']
  apply preimage_le_preimage_of_le f h

lemma image_preimage_eq_opensRange_inter (U : Y.Opens) : f ''·µÅ f ‚Åª¬π·µÅ U = f.opensRange ‚äì U := by
  apply Opens.ext
  simp [Set.image_preimage_eq_range_inter]

lemma image_injective : Function.Injective (f ''·µÅ ¬∑) := by
  intro U V hUV
  simpa using congrArg (f ‚Åª¬π·µÅ ¬∑) hUV

lemma image_iSup {Œπ : Sort*} (s : Œπ ‚Üí X.Opens) :
    (f ''·µÅ ‚®Ü (i : Œπ), s i) = ‚®Ü (i : Œπ), f ''·µÅ s i := by
  ext : 1
  simp [Set.image_iUnion]

lemma image_iSup‚ÇÇ {Œπ : Sort*} {Œ∫ : Œπ ‚Üí Sort*} (s : (i : Œπ) ‚Üí Œ∫ i ‚Üí X.Opens) :
    (f ''·µÅ ‚®Ü (i : Œπ), ‚®Ü (j : Œ∫ i), s i j) = ‚®Ü (i : Œπ), ‚®Ü (j : Œ∫ i), f ''·µÅ s i j := by
  ext : 1
  simp [Set.image_iUnion‚ÇÇ]

@[simp]
lemma map_mem_image_iff {X Y : Scheme} (f : X ‚ü∂ Y) [IsOpenImmersion f]
    {U : X.Opens} {x : X} : f.base x ‚àà f ''·µÅ U ‚Üî x ‚àà U :=
  f.isOpenEmbedding.injective.mem_set_image

@[simp]
lemma preimage_opensRange {X Y : Scheme.{u}} (f : X.Hom Y) [IsOpenImmersion f] :
    f ‚Åª¬π·µÅ f.opensRange = ‚ä§ := by
  simp [Scheme.Hom.opensRange]

lemma isIso_app (V : Y.Opens) (hV : V ‚â§ f.opensRange) : IsIso (f.app V) := by
  rw [show V = f ''·µÅ f ‚Åª¬π·µÅ V from Opens.ext (Set.image_preimage_eq_of_subset hV).symm]
  infer_instance

/-- The isomorphism `Œì(Y, f(U)) ‚âÖ Œì(X, U)` induced by an open immersion `f : X ‚ü∂ Y`. -/
def appIso (U) : Œì(Y, f ''·µÅ U) ‚âÖ Œì(X, U) :=
  (asIso <| LocallyRingedSpace.IsOpenImmersion.invApp f.toLRSHom U).symm

@[reassoc (attr := simp)]
theorem appIso_inv_naturality {U V : X.Opens} (i : op U ‚ü∂ op V) :
    X.presheaf.map i ‚â´ (f.appIso V).inv =
      (f.appIso U).inv ‚â´ Y.presheaf.map (f.opensFunctor.op.map i) :=
  PresheafedSpace.IsOpenImmersion.inv_naturality _ _

theorem appIso_hom (U) :
    (f.appIso U).hom = f.app (f ''·µÅ U) ‚â´ X.presheaf.map
      (eqToHom (preimage_image_eq f U).symm).op :=
  (PresheafedSpace.IsOpenImmersion.inv_invApp f.toPshHom U).trans (by rw [eqToHom_op])

theorem appIso_hom' (U) :
    (f.appIso U).hom = f.appLE (f ''·µÅ U) U (preimage_image_eq f U).ge :=
  f.appIso_hom U

@[reassoc (attr := simp)]
theorem app_appIso_inv (U) :
    f.app U ‚â´ (f.appIso (f ‚Åª¬π·µÅ U)).inv =
      Y.presheaf.map (homOfLE (Set.image_preimage_subset f.base U.1)).op :=
  PresheafedSpace.IsOpenImmersion.app_invApp _ _

/-- A variant of `app_invApp` that gives an `eqToHom` instead of `homOfLE`. -/
@[reassoc]
theorem app_invApp' (U) (hU : U ‚â§ f.opensRange) :
    f.app U ‚â´ (f.appIso (f ‚Åª¬π·µÅ U)).inv =
      Y.presheaf.map (eqToHom (Opens.ext <| by simpa [Set.image_preimage_eq_inter_range])).op :=
  PresheafedSpace.IsOpenImmersion.app_invApp _ _

@[reassoc (attr := simp), elementwise nosimp]
theorem appIso_inv_app (U) :
    (f.appIso U).inv ‚â´ f.app (f ''·µÅ U) = X.presheaf.map (eqToHom (preimage_image_eq f U)).op :=
  (PresheafedSpace.IsOpenImmersion.invApp_app _ _).trans (by rw [eqToHom_op])

@[reassoc (attr := simp), elementwise nosimp]
lemma appLE_appIso_inv {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [IsOpenImmersion f] {U : Y.Opens}
    {V : X.Opens} (e : V ‚â§ f ‚Åª¬π·µÅ U) :
    f.appLE U V e ‚â´ (f.appIso V).inv =
        Y.presheaf.map (homOfLE <| (f.image_le_image_of_le e).trans
          (f.image_preimage_eq_opensRange_inter U ‚ñ∏ inf_le_right)).op := by
  simp only [appLE, Category.assoc, appIso_inv_naturality, Functor.op_obj, Functor.op_map,
    Quiver.Hom.unop_op, opensFunctor_map_homOfLE, app_appIso_inv_assoc, Opens.carrier_eq_coe]
  rw [‚Üê Functor.map_comp]
  rfl

@[reassoc (attr := simp)]
lemma appIso_inv_appLE {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [IsOpenImmersion f] {U V : X.Opens}
    (e : V ‚â§ f ‚Åª¬π·µÅ f ''·µÅ U) :
    (f.appIso U).inv ‚â´ f.appLE (f ''·µÅ U) V e =
        X.presheaf.map (homOfLE (by rwa [preimage_image_eq] at e)).op := by
  simp only [appLE, appIso_inv_app_assoc, eqToHom_op]
  rw [‚Üê Functor.map_comp]
  rfl

end Scheme.Hom

/-- The open sets of an open subscheme corresponds to the open sets containing in the image. -/
@[simps]
def IsOpenImmersion.opensEquiv {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [IsOpenImmersion f] :
    X.Opens ‚âÉ { U : Y.Opens // U ‚â§ f.opensRange } where
  toFun U := ‚ü®f ''·µÅ U, Set.image_subset_range _ _‚ü©
  invFun U := f ‚Åª¬π·µÅ U
  left_inv _ := Opens.ext (Set.preimage_image_eq _ f.isOpenEmbedding.injective)
  right_inv U := Subtype.ext (Opens.ext (Set.image_preimage_eq_of_subset U.2))

namespace Scheme

instance basic_open_isOpenImmersion {R : CommRingCat.{u}} (f : R) :
    IsOpenImmersion (Spec.map (CommRingCat.ofHom (algebraMap R (Localization.Away f)))) := by
  apply SheafedSpace.IsOpenImmersion.of_stalk_iso (H := ?_)
  ¬∑ exact (PrimeSpectrum.localization_away_isOpenEmbedding (Localization.Away f) f :)
  ¬∑ intro x
    exact Spec_map_localization_isIso R (Submonoid.powers f) x

instance {R} [CommRing R] (f : R) :
    IsOpenImmersion (Spec.map (CommRingCat.ofHom (algebraMap R (Localization.Away f)))) :=
  basic_open_isOpenImmersion (R := .of R) f

lemma _root_.AlgebraicGeometry.IsOpenImmersion.of_isLocalization {R S} [CommRing R] [CommRing S]
    [Algebra R S] (f : R) [IsLocalization.Away f S] :
    IsOpenImmersion (Spec.map (CommRingCat.ofHom (algebraMap R S))) := by
  have e := (IsLocalization.algEquiv (.powers f) S
    (Localization.Away f)).symm.toAlgHom.comp_algebraMap
  rw [‚Üê e, CommRingCat.ofHom_comp, Spec.map_comp]
  have H : IsIso (CommRingCat.ofHom (IsLocalization.algEquiv
    (Submonoid.powers f) S (Localization.Away f)).symm.toAlgHom.toRingHom) := by
    exact inferInstanceAs (IsIso <| (IsLocalization.algEquiv
      (Submonoid.powers f) S (Localization.Away f)).toRingEquiv.toCommRingCatIso.inv)
  simp only [AlgEquiv.toAlgHom_eq_coe, AlgHom.toRingHom_eq_coe, AlgEquiv.toAlgHom_toRingHom] at H ‚ä¢
  infer_instance

theorem exists_affine_mem_range_and_range_subset
    {X : Scheme.{u}} {x : X} {U : X.Opens} (hxU : x ‚àà U) :
    ‚àÉ (R : CommRingCat) (f : Spec R ‚ü∂ X),
      IsOpenImmersion f ‚àß x ‚àà Set.range f.base ‚àß Set.range f.base ‚äÜ U := by
  obtain ‚ü®‚ü®V, hxV‚ü©, R, ‚ü®e‚ü©‚ü© := X.2 x
  have : e.hom.base ‚ü®x, hxV‚ü© ‚àà (Opens.map (e.inv.base ‚â´ V.inclusion')).obj U :=
    show ((e.hom ‚â´ e.inv).base ‚ü®x, hxV‚ü©).1 ‚àà U from e.hom_inv_id ‚ñ∏ hxU
  obtain ‚ü®_, ‚ü®_, ‚ü®r : R, rfl‚ü©, rfl‚ü©, hr, hr'‚ü© :=
    PrimeSpectrum.isBasis_basic_opens.exists_subset_of_mem_open this (Opens.is_open' _)
  let f : Spec(Localization.Away r) ‚ü∂ X :=
    Spec.map (CommRingCat.ofHom (algebraMap R (Localization.Away r))) ‚â´ ‚ü®e.inv ‚â´ X.ofRestrict _‚ü©
  refine ‚ü®.of (Localization.Away r), f, inferInstance, ?_‚ü©
  rw [Scheme.comp_base, TopCat.coe_comp, Set.range_comp]
  erw [PrimeSpectrum.localization_away_comap_range (Localization.Away r) r]
  exact ‚ü®‚ü®_, hr, congr(($(e.hom_inv_id).base ‚ü®x, hxV‚ü©).1)‚ü©, Set.image_subset_iff.mpr hr'‚ü©

end Scheme

namespace PresheafedSpace.IsOpenImmersion

section ToScheme

variable {X : PresheafedSpace CommRingCat.{u}} (Y : Scheme.{u})
variable (f : X ‚ü∂ Y.toPresheafedSpace) [H : PresheafedSpace.IsOpenImmersion f]

/-- If `X ‚ü∂ Y` is an open immersion, and `Y` is a scheme, then so is `X`. -/
def toScheme : Scheme := by
  apply LocallyRingedSpace.IsOpenImmersion.scheme (toLocallyRingedSpace _ f)
  intro x
  obtain ‚ü®R, i, _, h‚ÇÅ, h‚ÇÇ‚ü© :=
    Scheme.exists_affine_mem_range_and_range_subset (U := ‚ü®_, H.base_open.isOpen_range‚ü©) ‚ü®x, rfl‚ü©
  refine ‚ü®R, LocallyRingedSpace.IsOpenImmersion.lift (toLocallyRingedSpaceHom _ f) _ h‚ÇÇ, ?_, ?_‚ü©
  ¬∑ rw [LocallyRingedSpace.IsOpenImmersion.lift_range]; exact h‚ÇÅ
  ¬∑ delta LocallyRingedSpace.IsOpenImmersion.lift; infer_instance

@[simp]
theorem toScheme_toLocallyRingedSpace :
    (toScheme Y f).toLocallyRingedSpace = toLocallyRingedSpace Y.1 f :=
  rfl

/-- If `X ‚ü∂ Y` is an open immersion of PresheafedSpaces, and `Y` is a Scheme, we can
upgrade it into a morphism of Schemes.
-/
def toSchemeHom : toScheme Y f ‚ü∂ Y :=
  ‚ü®toLocallyRingedSpaceHom _ f‚ü©

@[simp]
theorem toSchemeHom_toPshHom : (toSchemeHom Y f).toPshHom = f :=
  rfl

instance toSchemeHom_isOpenImmersion : AlgebraicGeometry.IsOpenImmersion (toSchemeHom Y f) :=
  H

theorem scheme_eq_of_locallyRingedSpace_eq {X Y : Scheme.{u}}
    (H : X.toLocallyRingedSpace = Y.toLocallyRingedSpace) : X = Y := by
  cases X; cases Y; congr

theorem scheme_toScheme {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [AlgebraicGeometry.IsOpenImmersion f] :
    toScheme Y f.toPshHom = X := by
  apply scheme_eq_of_locallyRingedSpace_eq
  exact locallyRingedSpace_toLocallyRingedSpace f.toLRSHom

end ToScheme

end PresheafedSpace.IsOpenImmersion

section Restrict

variable {U : TopCat.{u}} (X : Scheme.{u}) {f : U ‚ü∂ TopCat.of X} (h : IsOpenEmbedding f)

/-- The restriction of a Scheme along an open embedding. -/
@[simps! -isSimp carrier, simps! presheaf_obj]
def Scheme.restrict : Scheme :=
  { PresheafedSpace.IsOpenImmersion.toScheme X (X.toPresheafedSpace.ofRestrict h) with
    toPresheafedSpace := X.toPresheafedSpace.restrict h }

lemma Scheme.restrict_toPresheafedSpace :
    (X.restrict h).toPresheafedSpace = X.toPresheafedSpace.restrict h := rfl

/-- The canonical map from the restriction to the subspace. -/
@[simps! toLRSHom_base, simps! -isSimp toLRSHom_c_app]
def Scheme.ofRestrict : X.restrict h ‚ü∂ X :=
  ‚ü®X.toLocallyRingedSpace.ofRestrict h‚ü©

@[simp]
lemma Scheme.ofRestrict_app (V) :
    (X.ofRestrict h).app V = X.presheaf.map (h.isOpenMap.adjunction.counit.app V).op  :=
  rfl

instance IsOpenImmersion.ofRestrict : IsOpenImmersion (X.ofRestrict h) :=
  show PresheafedSpace.IsOpenImmersion (X.toPresheafedSpace.ofRestrict h) by infer_instance

@[simp]
lemma Scheme.ofRestrict_appLE (V W e) :
    (X.ofRestrict h).appLE V W e = X.presheaf.map
      (homOfLE (show X.ofRestrict h ''·µÅ _ ‚â§ _ by exact Set.image_subset_iff.mpr e)).op := by
  dsimp [Hom.appLE]
  exact (X.presheaf.map_comp _ _).symm

@[simp]
lemma Scheme.ofRestrict_appIso (U) :
    (X.ofRestrict h).appIso U = Iso.refl _ := by
  ext1
  simp only [restrict_presheaf_obj, Hom.appIso_hom', ofRestrict_appLE, homOfLE_refl, op_id,
    CategoryTheory.Functor.map_id, Iso.refl_hom]

@[simp]
lemma Scheme.restrict_presheaf_map (V W) (i : V ‚ü∂ W) :
    (X.restrict h).presheaf.map i = X.presheaf.map (homOfLE (show X.ofRestrict h ''·µÅ W.unop ‚â§
      X.ofRestrict h ''·µÅ V.unop from Set.image_mono i.unop.le)).op := rfl

end Restrict

namespace IsOpenImmersion

variable {X Y Z : Scheme.{u}} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z)
variable [H : IsOpenImmersion f]

instance (priority := 100) of_isIso [IsIso g] : IsOpenImmersion g :=
  LocallyRingedSpace.IsOpenImmersion.of_isIso _

theorem to_iso {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [h : IsOpenImmersion f] [Epi f.base] : IsIso f :=
  @isIso_of_reflects_iso _ _ _ _ _ _ f
    (Scheme.forgetToLocallyRingedSpace ‚ãô
      LocallyRingedSpace.forgetToSheafedSpace ‚ãô SheafedSpace.forgetToPresheafedSpace)
    (@PresheafedSpace.IsOpenImmersion.to_iso _ _ _ _ f.toPshHom h _) _

theorem of_stalk_iso {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (hf : IsOpenEmbedding f.base)
    [‚àÄ x, IsIso (f.stalkMap x)] : IsOpenImmersion f :=
  haveI (x : X) : IsIso (f.toShHom.stalkMap x) := inferInstanceAs <| IsIso (f.stalkMap x)
  SheafedSpace.IsOpenImmersion.of_stalk_iso f.toShHom hf

instance stalk_iso {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [IsOpenImmersion f] (x : X) :
    IsIso (f.stalkMap x) :=
  inferInstanceAs <| IsIso (f.toLRSHom.stalkMap x)

lemma of_comp {X Y Z : Scheme.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsOpenImmersion g]
    [IsOpenImmersion (f ‚â´ g)] : IsOpenImmersion f :=
  haveI (x : X) : IsIso (f.stalkMap x) :=
    haveI : IsIso (g.stalkMap (f.base x) ‚â´ f.stalkMap x) := by
      rw [‚Üê Scheme.stalkMap_comp]
      infer_instance
    IsIso.of_isIso_comp_left (f := g.stalkMap (f.base x)) _
  IsOpenImmersion.of_stalk_iso _ <|
    IsOpenEmbedding.of_comp _ (Scheme.Hom.isOpenEmbedding g) (Scheme.Hom.isOpenEmbedding (f ‚â´ g))

theorem iff_stalk_iso {X Y : Scheme.{u}} (f : X ‚ü∂ Y) :
    IsOpenImmersion f ‚Üî IsOpenEmbedding f.base ‚àß ‚àÄ x, IsIso (f.stalkMap x) :=
  ‚ü®fun H => ‚ü®H.1, fun x ‚Ü¶ inferInstanceAs <| IsIso (f.toPshHom.stalkMap x)‚ü©,
    fun ‚ü®h, _‚ü© => IsOpenImmersion.of_stalk_iso f h‚ü©

theorem _root_.AlgebraicGeometry.isIso_iff_isOpenImmersion {X Y : Scheme.{u}} (f : X ‚ü∂ Y) :
    IsIso f ‚Üî IsOpenImmersion f ‚àß Epi f.base :=
  ‚ü®fun _ => ‚ü®inferInstance, inferInstance‚ü©, fun ‚ü®_, _‚ü© => IsOpenImmersion.to_iso f‚ü©

theorem _root_.AlgebraicGeometry.isIso_iff_stalk_iso {X Y : Scheme.{u}} (f : X ‚ü∂ Y) :
    IsIso f ‚Üî IsIso f.base ‚àß ‚àÄ x, IsIso (f.stalkMap x) := by
  rw [isIso_iff_isOpenImmersion, IsOpenImmersion.iff_stalk_iso, and_comm, ‚Üê and_assoc]
  refine and_congr ‚ü®?_, ?_‚ü© Iff.rfl
  ¬∑ rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
    convert_to
      IsIso
        (TopCat.isoOfHomeo
          (Equiv.toHomeomorphOfContinuousOpen
            (.ofBijective _ ‚ü®h‚ÇÇ.injective, (TopCat.epi_iff_surjective _).mp h‚ÇÅ‚ü©) h‚ÇÇ.continuous
            h‚ÇÇ.isOpenMap)).hom
    infer_instance
  ¬∑ intro H; exact ‚ü®inferInstance, (TopCat.homeoOfIso (asIso f.base)).isOpenEmbedding‚ü©

/-- An open immersion induces an isomorphism from the domain onto the image -/
def isoRestrict : X ‚âÖ (Z.restrict H.base_open :) :=
  Scheme.fullyFaithfulForgetToLocallyRingedSpace.preimageIso
    (LocallyRingedSpace.IsOpenImmersion.isoRestrict f.toLRSHom)

local notation "forget" => Scheme.forgetToLocallyRingedSpace

instance mono : Mono f :=
  Scheme.forgetToLocallyRingedSpace.mono_of_mono_map
    (show Mono f.toLRSHom by infer_instance)

lemma le_monomorphisms :
    IsOpenImmersion ‚â§ MorphismProperty.monomorphisms Scheme.{u} := fun _ _ _ _ ‚Ü¶
  MorphismProperty.monomorphisms.infer_property _

instance forget_map_isOpenImmersion : LocallyRingedSpace.IsOpenImmersion ((forget).map f) :=
  ‚ü®H.base_open, H.c_iso‚ü©

instance hasLimit_cospan_forget_of_left :
    HasLimit (cospan f g ‚ãô Scheme.forgetToLocallyRingedSpace) := by
  rw [hasLimit_iff_of_iso (diagramIsoCospan _)]
  exact inferInstanceAs (HasLimit (cospan ((forget).map f) ((forget).map g)))

open CategoryTheory.Limits.WalkingCospan

instance hasLimit_cospan_forget_of_left' :
    HasLimit (cospan ((cospan f g ‚ãô forget).map Hom.inl) ((cospan f g ‚ãô forget).map Hom.inr)) :=
  show HasLimit (cospan ((forget).map f) ((forget).map g)) from inferInstance

instance hasLimit_cospan_forget_of_right : HasLimit (cospan g f ‚ãô forget) := by
  rw [hasLimit_iff_of_iso (diagramIsoCospan _)]
  exact inferInstanceAs (HasLimit (cospan ((forget).map g) ((forget).map f)))

instance hasLimit_cospan_forget_of_right' :
    HasLimit (cospan ((cospan g f ‚ãô forget).map Hom.inl) ((cospan g f ‚ãô forget).map Hom.inr)) :=
  show HasLimit (cospan ((forget).map g) ((forget).map f)) from inferInstance

instance forgetCreatesPullbackOfLeft : CreatesLimit (cospan f g) forget :=
  createsLimitOfFullyFaithfulOfIso
    (PresheafedSpace.IsOpenImmersion.toScheme Y (pullback.snd f.toLRSHom g.toLRSHom).toShHom)
    (eqToIso (by simp) ‚â™‚â´ HasLimit.isoOfNatIso (diagramIsoCospan _).symm)

instance forgetCreatesPullbackOfRight : CreatesLimit (cospan g f) forget :=
  createsLimitOfFullyFaithfulOfIso
    (PresheafedSpace.IsOpenImmersion.toScheme Y (pullback.fst g.toLRSHom f.toLRSHom).1)
    (eqToIso (by simp) ‚â™‚â´ HasLimit.isoOfNatIso (diagramIsoCospan _).symm)

instance forget_preservesOfLeft : PreservesLimit (cospan f g) forget :=
  CategoryTheory.preservesLimit_of_createsLimit_and_hasLimit _ _

instance forget_preservesOfRight : PreservesLimit (cospan g f) forget :=
  preservesPullback_symmetry _ _ _

instance hasPullback_of_left : HasPullback f g :=
  hasLimit_of_created (cospan f g) forget

instance hasPullback_of_right : HasPullback g f :=
  hasLimit_of_created (cospan g f) forget

instance pullback_snd_of_left : IsOpenImmersion (pullback.snd f g) := by
  have := PreservesPullback.iso_hom_snd forget f g
  dsimp only [Scheme.forgetToLocallyRingedSpace, inducedFunctor_map] at this
  change LocallyRingedSpace.IsOpenImmersion _
  rw [‚Üê this]
  infer_instance

instance pullback_fst_of_right : IsOpenImmersion (pullback.fst g f) := by
  rw [‚Üê pullbackSymmetry_hom_comp_snd]
  infer_instance

instance pullback_to_base [IsOpenImmersion g] :
    IsOpenImmersion (limit.œÄ (cospan f g) WalkingCospan.one) := by
  rw [‚Üê limit.w (cospan f g) WalkingCospan.Hom.inl]
  change IsOpenImmersion (_ ‚â´ f)
  infer_instance

instance forgetToTop_preserves_of_left : PreservesLimit (cospan f g) Scheme.forgetToTop := by
  delta Scheme.forgetToTop
  refine @Limits.comp_preservesLimit _ _ _ _ _ _ (K := cospan f g) _ _ (F := forget)
    (G := LocallyRingedSpace.forgetToTop) ?_ ?_
  ¬∑ infer_instance
  refine @preservesLimit_of_iso_diagram _ _ _ _ _ _ _ _ _ (diagramIsoCospan.{u} _).symm ?_
  dsimp [LocallyRingedSpace.forgetToTop]
  infer_instance

instance forgetToTop_preserves_of_right : PreservesLimit (cospan g f) Scheme.forgetToTop :=
  preservesPullback_symmetry _ _ _

instance : PreservesLimit (cospan f g) Scheme.forget := by delta Scheme.forget; infer_instance

instance : PreservesLimit (cospan g f) Scheme.forget := by delta Scheme.forget; infer_instance

theorem range_pullback_snd_of_left :
    Set.range (pullback.snd f g).base = (g ‚Åª¬π·µÅ f.opensRange).1 := by
  rw [‚Üê show _ = (pullback.snd f g).base from
    PreservesPullback.iso_hom_snd Scheme.forgetToTop f g, TopCat.coe_comp, Set.range_comp,
    Set.range_eq_univ.mpr, ‚Üê @Set.preimage_univ _ _ (pullback.fst f.base g.base)]
  -- Porting note (https://github.com/leanprover-community/mathlib4/issues/11224): was `rw`
  ¬∑ erw [TopCat.pullback_snd_image_fst_preimage]
    rw [Set.image_univ]
    rfl
  rw [‚Üê TopCat.epi_iff_surjective]
  infer_instance

theorem opensRange_pullback_snd_of_left :
    (pullback.snd f g).opensRange = g ‚Åª¬π·µÅ f.opensRange :=
  Opens.ext (range_pullback_snd_of_left f g)

theorem range_pullback_fst_of_right :
    Set.range (pullback.fst g f).base =
      ((Opens.map g.base).obj ‚ü®Set.range f.base, H.base_open.isOpen_range‚ü©).1 := by
  rw [‚Üê show _ = (pullback.fst g f).base from
    PreservesPullback.iso_hom_fst Scheme.forgetToTop g f, TopCat.coe_comp, Set.range_comp,
    Set.range_eq_univ.mpr, ‚Üê @Set.preimage_univ _ _ (pullback.snd g.base f.base)]
  -- Porting note (https://github.com/leanprover-community/mathlib4/issues/11224): was `rw`
  ¬∑ erw [TopCat.pullback_fst_image_snd_preimage]
    rw [Set.image_univ]
    rfl
  rw [‚Üê TopCat.epi_iff_surjective]
  infer_instance

theorem opensRange_pullback_fst_of_right :
    (pullback.fst g f).opensRange = g ‚Åª¬π·µÅ f.opensRange :=
  Opens.ext (range_pullback_fst_of_right f g)

theorem range_pullback_to_base_of_left :
    Set.range (pullback.fst f g ‚â´ f).base =
      Set.range f.base ‚à© Set.range g.base := by
  rw [pullback.condition, Scheme.comp_base, TopCat.coe_comp, Set.range_comp,
    range_pullback_snd_of_left, Opens.carrier_eq_coe, Opens.map_obj, Opens.coe_mk,
    Set.image_preimage_eq_inter_range, Opens.carrier_eq_coe, Scheme.Hom.coe_opensRange]

theorem range_pullback_to_base_of_right :
    Set.range (pullback.fst g f ‚â´ g).base =
      Set.range g.base ‚à© Set.range f.base := by
  rw [Scheme.comp_base, TopCat.coe_comp, Set.range_comp, range_pullback_fst_of_right,
    Opens.map_obj, Opens.carrier_eq_coe, Opens.coe_mk, Set.image_preimage_eq_inter_range,
    Set.inter_comm]

lemma image_preimage_eq_preimage_image_of_isPullback {X Y U V : Scheme.{u}}
    {f : X ‚ü∂ Y} {f' : U ‚ü∂ V} {iU : U ‚ü∂ X} {iV : V ‚ü∂ Y} [IsOpenImmersion iV] [IsOpenImmersion iU]
    (H : IsPullback f' iU iV f) (W : V.Opens) : iU ''·µÅ f' ‚Åª¬π·µÅ W = f ‚Åª¬π·µÅ iV ''·µÅ W := by
  ext x
  by_cases hx : x ‚àà Set.range iU.base
  ¬∑ obtain ‚ü®x, rfl‚ü© := hx
    simp only [IsOpenMap.coe_functor_obj, TopologicalSpace.Opens.map_coe,
      iU.isOpenEmbedding.injective.mem_set_image, Set.mem_preimage, SetLike.mem_coe,
      ‚Üê Scheme.comp_base_apply, ‚Üê H.w]
    simp only [Scheme.comp_coeBase, TopCat.comp_app,
      iV.isOpenEmbedding.injective.mem_set_image, SetLike.mem_coe]
  ¬∑ constructor
    ¬∑ rintro ‚ü®x, hx, rfl‚ü©; cases hx ‚ü®x, rfl‚ü©
    ¬∑ rintro ‚ü®y, hy, e : iV.base y = f.base x‚ü©
      obtain ‚ü®x, rfl‚ü© := (IsOpenImmersion.range_pullback_snd_of_left iV f).ge ‚ü®y, e‚ü©
      rw [‚Üê H.isoPullback_inv_snd] at hx
      cases hx ‚ü®_, rfl‚ü©

/-- The universal property of open immersions:
For an open immersion `f : X ‚ü∂ Z`, given any morphism of schemes `g : Y ‚ü∂ Z` whose topological
image is contained in the image of `f`, we can lift this morphism to a unique `Y ‚ü∂ X` that
commutes with these maps.
-/
def lift (H' : Set.range g.base ‚äÜ Set.range f.base) : Y ‚ü∂ X :=
  ‚ü®LocallyRingedSpace.IsOpenImmersion.lift f.toLRSHom g.toLRSHom H'‚ü©

@[reassoc (attr := simp)]
theorem lift_fac (H' : Set.range g.base ‚äÜ Set.range f.base) : lift f g H' ‚â´ f = g :=
  Scheme.Hom.ext' <| LocallyRingedSpace.IsOpenImmersion.lift_fac f.toLRSHom g.toLRSHom H'

theorem lift_uniq (H' : Set.range g.base ‚äÜ Set.range f.base) (l : Y ‚ü∂ X) (hl : l ‚â´ f = g) :
    l = lift f g H' :=
  Scheme.Hom.ext' <| LocallyRingedSpace.IsOpenImmersion.lift_uniq
    f.toLRSHom g.toLRSHom H' l.toLRSHom congr(($hl).toLRSHom)

@[reassoc]
lemma comp_lift {Y' : Scheme} (g' : Y' ‚ü∂ Y) (H : Set.range g.base ‚äÜ Set.range f.base) :
    g' ‚â´ lift f g H = lift f (g' ‚â´ g) (.trans (by simp [Set.range_comp_subset_range]) H) := by
  simp [‚Üê cancel_mono f]

theorem isPullback_lift_id
    {X U Y : Scheme.{u}} (f : X ‚ü∂ Y) (g : U ‚ü∂ Y) [IsOpenImmersion g]
    (H : Set.range f.base ‚äÜ Set.range g.base) :
    IsPullback (IsOpenImmersion.lift g f H) (ùüô _) g f := by
  convert IsPullback.of_id_snd.paste_horiz (IsKernelPair.id_of_mono g)
  ¬∑ exact (Category.comp_id _).symm
  ¬∑ simp

/-- Two open immersions with equal range are isomorphic. -/
def isoOfRangeEq [IsOpenImmersion g] (e : Set.range f.base = Set.range g.base) : X ‚âÖ Y where
  hom := lift g f (le_of_eq e)
  inv := lift f g (le_of_eq e.symm)
  hom_inv_id := by rw [‚Üê cancel_mono f]; simp
  inv_hom_id := by rw [‚Üê cancel_mono g]; simp

@[reassoc (attr := simp)]
lemma isoOfRangeEq_hom_fac {X Y Z : Scheme.{u}} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z)
    [IsOpenImmersion f] [IsOpenImmersion g] (e : Set.range f.base = Set.range g.base) :
    (isoOfRangeEq f g e).hom ‚â´ g = f :=
  lift_fac _ _ (le_of_eq e)

@[reassoc (attr := simp)]
lemma isoOfRangeEq_inv_fac {X Y Z : Scheme.{u}} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z)
    [IsOpenImmersion f] [IsOpenImmersion g] (e : Set.range f.base = Set.range g.base) :
    (isoOfRangeEq f g e).inv ‚â´ f = g :=
  lift_fac _ _ (le_of_eq e.symm)

theorem app_eq_invApp_app_of_comp_eq_aux {X Y U : Scheme.{u}} (f : Y ‚ü∂ U) (g : U ‚ü∂ X) (fg : Y ‚ü∂ X)
    (H : fg = f ‚â´ g) [h : IsOpenImmersion g] (V : U.Opens) :
    f ‚Åª¬π·µÅ V = fg ‚Åª¬π·µÅ (g ''·µÅ V) := by
  simp_all

/-- The `fg` argument is to avoid nasty stuff about dependent types. -/
theorem app_eq_appIso_inv_app_of_comp_eq {X Y U : Scheme.{u}} (f : Y ‚ü∂ U) (g : U ‚ü∂ X) (fg : Y ‚ü∂ X)
    (H : fg = f ‚â´ g) [h : IsOpenImmersion g] (V : U.Opens) :
    f.app V = (g.appIso V).inv ‚â´ fg.app (g ''·µÅ V) ‚â´ Y.presheaf.map
      (eqToHom <| IsOpenImmersion.app_eq_invApp_app_of_comp_eq_aux f g fg H V).op := by
  subst H
  rw [Scheme.comp_app, Category.assoc, Scheme.Hom.appIso_inv_app_assoc, f.naturality_assoc,
    ‚Üê Functor.map_comp, ‚Üê op_comp, Quiver.Hom.unop_op, eqToHom_map, eqToHom_trans,
    eqToHom_op, eqToHom_refl, CategoryTheory.Functor.map_id, Category.comp_id]

theorem lift_app {X Y U : Scheme.{u}} (f : U ‚ü∂ Y) (g : X ‚ü∂ Y) [IsOpenImmersion f] (H)
    (V : U.Opens) :
    (IsOpenImmersion.lift f g H).app V = (f.appIso V).inv ‚â´ g.app (f ''·µÅ V) ‚â´
      X.presheaf.map (eqToHom <| IsOpenImmersion.app_eq_invApp_app_of_comp_eq_aux _ _ _
        (IsOpenImmersion.lift_fac f g H).symm V).op :=
  IsOpenImmersion.app_eq_appIso_inv_app_of_comp_eq _ _ _ (lift_fac _ _ _).symm _

/-- If `f` is an open immersion `X ‚ü∂ Y`, the global sections of `X`
are naturally isomorphic to the sections of `Y` over the image of `f`. -/
noncomputable
def ŒìIso {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [IsOpenImmersion f] (U : Y.Opens) :
    Œì(X, f‚Åª¬π·µÅ U) ‚âÖ Œì(Y, f.opensRange ‚äì U) :=
  (f.appIso (f‚Åª¬π·µÅ U)).symm ‚â™‚â´
    Y.presheaf.mapIso (eqToIso <| (f.image_preimage_eq_opensRange_inter U).symm).op

@[simp]
lemma ŒìIso_inv {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [IsOpenImmersion f] (U : Y.Opens) :
    (ŒìIso f U).inv = f.appLE (f.opensRange ‚äì U) (f‚Åª¬π·µÅ U)
      (by rw [‚Üê f.image_preimage_eq_opensRange_inter, f.preimage_image_eq]) := by
  simp only [ŒìIso, Iso.trans_inv, Functor.mapIso_inv, Iso.op_inv, eqToIso.inv, eqToHom_op,
    Iso.symm_inv, Scheme.Hom.appIso_hom', Scheme.Hom.map_appLE]

@[reassoc, elementwise]
lemma map_ŒìIso_inv {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [IsOpenImmersion f] (U : Y.Opens) :
    Y.presheaf.map (homOfLE inf_le_right).op ‚â´ (ŒìIso f U).inv = f.app U := by
  simp [Scheme.Hom.appLE_eq_app]

@[reassoc, elementwise]
lemma ŒìIso_hom_map {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [IsOpenImmersion f] (U : Y.Opens) :
    f.app U ‚â´ (ŒìIso f U).hom = Y.presheaf.map (homOfLE inf_le_right).op := by
  rw [‚Üê map_ŒìIso_inv]
  simp [-ŒìIso_inv]

/-- Given an open immersion `f : U ‚ü∂ X`, the isomorphism between global sections
  of `U` and the sections of `X` at the image of `f`. -/
noncomputable
def ŒìIsoTop {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [IsOpenImmersion f] :
    Œì(X, ‚ä§) ‚âÖ Œì(Y, f.opensRange) :=
  (f.appIso ‚ä§).symm ‚â™‚â´ Y.presheaf.mapIso (eqToIso f.image_top_eq_opensRange.symm).op

instance {Z : Scheme.{u}} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [IsOpenImmersion f]
    (H' : Set.range g.base ‚äÜ Set.range f.base) [IsOpenImmersion g] :
    IsOpenImmersion (IsOpenImmersion.lift f g H') :=
  haveI : IsOpenImmersion (IsOpenImmersion.lift f g H' ‚â´ f) := by simpa
  IsOpenImmersion.of_comp _ f

end IsOpenImmersion

lemma isIso_of_isOpenImmersion_of_opensRange_eq_top {X Y : Scheme.{u}} (f : X ‚ü∂ Y)
    [IsOpenImmersion f] (hf : f.opensRange = ‚ä§) : IsIso f := by
  rw [isIso_iff_isOpenImmersion]
  refine ‚ü®inferInstance, ?_‚ü©
  rw [TopCat.epi_iff_surjective, ‚Üê Set.range_eq_univ]
  exact TopologicalSpace.Opens.ext_iff.mp hf

section MorphismProperty

instance isOpenImmersion_isStableUnderComposition :
    MorphismProperty.IsStableUnderComposition @IsOpenImmersion where
  comp_mem f g _ _ := LocallyRingedSpace.IsOpenImmersion.comp f.toLRSHom g.toLRSHom

instance isOpenImmersion_respectsIso : MorphismProperty.RespectsIso @IsOpenImmersion := by
  apply MorphismProperty.respectsIso_of_isStableUnderComposition
  intro _ _ f (hf : IsIso f)
  have : IsIso f := hf
  infer_instance

instance isOpenImmersion_isMultiplicative :
    MorphismProperty.IsMultiplicative @IsOpenImmersion where
  id_mem _ := inferInstance

instance isOpenImmersion_stableUnderBaseChange :
    MorphismProperty.IsStableUnderBaseChange @IsOpenImmersion :=
  MorphismProperty.IsStableUnderBaseChange.mk' <| by
    intro X Y Z f g _ H; infer_instance

end MorphismProperty

namespace Scheme

variable {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [H : IsOpenImmersion f]

theorem image_basicOpen {U : X.Opens} (r : Œì(X, U)) :
    f ''·µÅ X.basicOpen r = Y.basicOpen ((f.appIso U).inv r) := by
  have e := Scheme.preimage_basicOpen f ((f.appIso U).inv r)
  rw [Scheme.Hom.appIso_inv_app_apply, Scheme.basicOpen_res, inf_eq_right.mpr _] at e
  ¬∑ rw [‚Üê e, f.image_preimage_eq_opensRange_inter, inf_eq_right]
    refine Set.Subset.trans (Scheme.basicOpen_le _ _) (Set.image_subset_range _ _)
  ¬∑ exact (X.basicOpen_le r).trans (f.preimage_image_eq _).ge

lemma image_zeroLocus {U : X.Opens} (s : Set Œì(X, U)) :
    f.base '' X.zeroLocus s =
      Y.zeroLocus (U := f ''·µÅ U) ((f.appIso U).inv.hom '' s) ‚à© Set.range f.base := by
  ext x
  by_cases hx : x ‚àà Set.range f.base
  ¬∑ obtain ‚ü®x, rfl‚ü© := hx
    simp only [f.isOpenEmbedding.injective.mem_set_image, Scheme.mem_zeroLocus_iff,
      ‚Üê SetLike.mem_coe, Set.mem_inter_iff, Set.forall_mem_image, ‚Üê Scheme.image_basicOpen,
      IsOpenMap.coe_functor_obj, Set.mem_range, exists_apply_eq_apply, and_true]
  ¬∑ simp only [Set.mem_inter_iff, hx, and_false, iff_false]
    exact fun H ‚Ü¶ hx (Set.image_subset_range _ _ H)

end Scheme

end AlgebraicGeometry
