/-
Copyright (c) 2024 Christian Merten, Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten, Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Sites.MorphismProperty
import Mathlib.CategoryTheory.MorphismProperty.Limits

/-!
# Covers of schemes

This file provides the basic API for covers of schemes. A cover of a scheme `X` with respect to
a morphism property `P` is a jointly surjective indexed family of scheme morphisms with
target `X` all satisfying `P`.

## Implementation details

The definition on the pullback of a cover along a morphism depends on results that
are developed later in the import tree. Hence in this file, they have additional assumptions
that will be automatically satisfied in later files. The motivation here is that we already
know that these assumptions are satisfied for open immersions and hence the cover API for open
immersions can be used to deduce these assumptions in the general case.

-/


noncomputable section

open TopologicalSpace CategoryTheory Opposite CategoryTheory.Limits

universe v vâ‚ vâ‚‚ u

namespace AlgebraicGeometry

namespace Scheme

variable (K : Precoverage Scheme.{u})

/-- A coverage `K` on `Scheme` is called jointly surjective if every covering family in `K`
is jointly surjective. -/
class JointlySurjective (K : Precoverage Scheme.{u}) : Prop where
  exists_eq {X : Scheme.{u}} (S : Presieve X) (hS : S âˆˆ K X) (x : X) :
    âˆƒ (Y : Scheme.{u}) (g : Y âŸ¶ X), S g âˆ§ x âˆˆ Set.range g

/-- A cover of `X` in the coverage `K` is a `0`-hypercover for `K`. -/
abbrev Cover (K : Precoverage Scheme.{u}) := Precoverage.ZeroHypercover.{v} K

variable {K}

variable {X Y Z : Scheme.{u}} (ğ’° : X.Cover K) (f : X âŸ¶ Z) (g : Y âŸ¶ Z)
variable [âˆ€ x, HasPullback (ğ’°.f x â‰« f) g]

lemma Cover.exists_eq [JointlySurjective K] (ğ’° : X.Cover K) (x : X) :
    âˆƒ i y, ğ’°.f i y = x := by
  obtain âŸ¨Y, g, âŸ¨iâŸ©, y, hyâŸ© := JointlySurjective.exists_eq ğ’°.presieveâ‚€ ğ’°.memâ‚€ x
  use i, y

/-- A choice of an index `i` such that `x` is in the range of `ğ’°.f i`. -/
def Cover.idx [JointlySurjective K] (ğ’° : X.Cover K) (x : X) : ğ’°.Iâ‚€ :=
  (ğ’°.exists_eq x).choose

lemma Cover.covers [JointlySurjective K] (ğ’° : X.Cover K) (x : X) :
    x âˆˆ Set.range (ğ’°.f (ğ’°.idx x)) :=
  (ğ’°.exists_eq x).choose_spec

theorem Cover.iUnion_range [JointlySurjective K] {X : Scheme.{u}} (ğ’° : X.Cover K) :
    â‹ƒ i, Set.range (ğ’°.f i) = Set.univ := by
  rw [Set.eq_univ_iff_forall]
  intro x
  rw [Set.mem_iUnion]
  exact ğ’°.exists_eq x

instance Cover.nonempty_of_nonempty [JointlySurjective K] [Nonempty X] (ğ’° : X.Cover K) :
    Nonempty ğ’°.Iâ‚€ := by
  obtain âŸ¨i, _âŸ© := ğ’°.exists_eq â€¹Nonempty Xâ€º.some
  use i

section MorphismProperty

variable {P Q : MorphismProperty Scheme.{u}}

lemma presieveâ‚€_mem_precoverage_iff (E : PreZeroHypercover X) :
    E.presieveâ‚€ âˆˆ precoverage P X â†” (âˆ€ x, âˆƒ i, x âˆˆ Set.range (E.f i)) âˆ§ âˆ€ i, P (E.f i) := by
  simp

@[grind â†]
lemma Cover.map_prop (ğ’° : X.Cover (precoverage P)) (i : ğ’°.Iâ‚€) : P (ğ’°.f i) :=
  ğ’°.memâ‚€.2 âŸ¨iâŸ©

/-- Given a family of schemes with morphisms to `X` satisfying `P` that jointly
cover `X`, `Cover.mkOfCovers` is an associated `P`-cover of `X`. -/
@[simps!]
def Cover.mkOfCovers (J : Type*) (obj : J â†’ Scheme.{u}) (map : (j : J) â†’ obj j âŸ¶ X)
    (covers : âˆ€ x, âˆƒ j y, map j y = x)
    (map_prop : âˆ€ j, P (map j) := by infer_instance) : X.Cover (precoverage P) where
  Iâ‚€ := J
  X := obj
  f := map
  memâ‚€ := by
    simp_rw [presieveâ‚€_mem_precoverage_iff, Set.mem_range]
    grind

/-- An isomorphism `X âŸ¶ Y` is a `P`-cover of `Y`. -/
@[simps! Iâ‚€ X f]
def coverOfIsIso [P.ContainsIdentities] [P.RespectsIso] {X Y : Scheme.{u}} (f : X âŸ¶ Y)
    [IsIso f] : Cover.{v} (precoverage P) Y :=
  .mkOfCovers PUnit (fun _ â†¦ X)
    (fun _ â†¦ f)
    (fun x â†¦ âŸ¨âŸ¨âŸ©, inv f x, by simp [â† Hom.comp_apply]âŸ©)
    (fun _ â†¦ P.of_isIso f)

instance : JointlySurjective (precoverage P) where
  exists_eq {X} R := fun âŸ¨hR, _âŸ© x â†¦ by
    rw [jointlySurjectivePrecoverage, Presieve.mem_comap_jointlySurjectivePrecoverage_iff] at hR
    obtain âŸ¨Y, g, hg, heqâŸ© := hR x
    use Y, g, hg
    exact heq

/-- Turn a `K`-cover into a `Q`-cover by showing that the components satisfy `Q`. -/
def Cover.changeProp [JointlySurjective K] (ğ’° : X.Cover K) (h : âˆ€ j, Q (ğ’°.f j)) :
    X.Cover (precoverage Q) where
  Iâ‚€ := ğ’°.Iâ‚€
  X := ğ’°.X
  f := ğ’°.f
  memâ‚€ := by
    rw [presieveâ‚€_mem_precoverage_iff]
    exact âŸ¨ğ’°.exists_eq, hâŸ©

/-- We construct a cover from another, by providing the needed fields and showing that the
provided fields are isomorphic with the original cover. -/
@[simps Iâ‚€ X f]
def Cover.copy [P.RespectsIso] {X : Scheme.{u}} (ğ’° : X.Cover (precoverage P))
    (J : Type*) (obj : J â†’ Scheme)
    (map : âˆ€ i, obj i âŸ¶ X) (eâ‚ : J â‰ƒ ğ’°.Iâ‚€) (eâ‚‚ : âˆ€ i, obj i â‰… ğ’°.X (eâ‚ i))
    (h : âˆ€ i, map i = (eâ‚‚ i).hom â‰« ğ’°.f (eâ‚ i)) : X.Cover (precoverage P) where
  Iâ‚€ := J
  X := obj
  f := map
  memâ‚€ := by
    rw [presieveâ‚€_mem_precoverage_iff]
    refine âŸ¨fun x â†¦ ?_, ?_âŸ©
    Â· obtain âŸ¨i, y, rflâŸ© := ğ’°.exists_eq x
      obtain âŸ¨i, rflâŸ© := eâ‚.surjective i
      use i, (eâ‚‚ i).inv y
      simp [h]
    Â· simp_rw [h, MorphismProperty.cancel_left_of_respectsIso]
      intro i
      exact ğ’°.map_prop _

/-- The pushforward of a cover along an isomorphism. -/
@[simps! Iâ‚€ X f]
def Cover.pushforwardIso [P.RespectsIso] [P.ContainsIdentities] [P.IsStableUnderComposition]
    {X Y : Scheme.{u}} (ğ’° : Cover.{v} (precoverage P) X) (f : X âŸ¶ Y) [IsIso f] :
    Cover.{v} (precoverage P) Y :=
  Cover.copy ((coverOfIsIso.{v, u} f).bind fun _ => ğ’°) ğ’°.Iâ‚€ _ _
    ((Equiv.punitProd _).symm.trans (Equiv.sigmaEquivProd PUnit ğ’°.Iâ‚€).symm) (fun _ => Iso.refl _)
    fun _ => (Category.id_comp _).symm

/-- Adding map satisfying `P` into a cover gives another cover. -/
@[simps toPreZeroHypercover]
nonrec def Cover.add {X Y : Scheme.{u}} (ğ’° : X.Cover (precoverage P)) (f : Y âŸ¶ X)
    (hf : P f := by infer_instance) : X.Cover (precoverage P) where
  __ := ğ’°.toPreZeroHypercover.add f
  memâ‚€ := by
    rw [presieveâ‚€_mem_precoverage_iff]
    refine âŸ¨fun x â†¦ âŸ¨some <| ğ’°.idx x, ğ’°.covers xâŸ©, ?_âŸ©
    rintro (i|i) <;> simp [hf, ğ’°.map_prop]

@[deprecated (since := "2025-10-02")]
alias Cover.pullbackCover := Precoverage.ZeroHypercover.pullbackâ‚

/-- The family of morphisms from the pullback cover to the original cover. -/
def Cover.pullbackHom [P.IsStableUnderBaseChange] [IsJointlySurjectivePreserving P]
    {X W : Scheme.{u}} (ğ’° : X.Cover (precoverage P)) (f : W âŸ¶ X) (i) [âˆ€ x, HasPullback f (ğ’°.f x)] :
    (ğ’°.pullbackâ‚ f).X i âŸ¶ ğ’°.X i :=
  pullback.snd f (ğ’°.f i)

@[reassoc (attr := simp)]
lemma Cover.pullbackHom_map [P.IsStableUnderBaseChange] [IsJointlySurjectivePreserving P]
    {X W : Scheme.{u}} (ğ’° : X.Cover (precoverage P)) (f : W âŸ¶ X)
    [âˆ€ (x : ğ’°.Iâ‚€), HasPullback f (ğ’°.f x)] (i) :
    ğ’°.pullbackHom f i â‰« ğ’°.f i = (ğ’°.pullbackâ‚ f).f i â‰« f := pullback.condition.symm

@[deprecated (since := "2025-10-02")]
alias Cover.pullbackCover' := Precoverage.ZeroHypercover.pullbackâ‚‚

/--
An affine cover of `X` consists of a jointly surjective family of maps into `X` from
spectra of rings.

Note: The `map_prop` field is equipped with a default argument `by infer_instance`. In general
this causes worse error messages, but in practice `P` is mostly defined via `class`.
-/
structure AffineCover (P : MorphismProperty Scheme.{u}) (S : Scheme.{u}) where
  /-- index set of an affine cover of a scheme `S` -/
  Iâ‚€ : Type v
  /-- the ring associated to a component of an affine cover -/
  X (j : Iâ‚€) : CommRingCat.{u}
  /-- the components map to `S` -/
  f (j : Iâ‚€) : Spec (X j) âŸ¶ S
  /-- given a point of `x : S`, `idx x` is the index of the component which contains `x` -/
  idx (x : S) : Iâ‚€
  /-- the components cover `S` -/
  covers (x : S) : x âˆˆ Set.range (f (idx x))
  /-- the component maps satisfy `P` -/
  map_prop (j : Iâ‚€) : P (f j) := by infer_instance

@[deprecated (since := "2025-09-19")]
alias AffineCover.J := AffineCover.Iâ‚€

@[deprecated (since := "2025-09-19")]
alias AffineCover.obj := AffineCover.X

@[deprecated (since := "2025-09-19")]
alias AffineCover.map := AffineCover.f

/-- The cover associated to an affine cover. -/
@[simps]
def AffineCover.cover {X : Scheme.{u}} (ğ’° : X.AffineCover P) :
    X.Cover (precoverage P) where
  Iâ‚€ := ğ’°.Iâ‚€
  X j := Spec (ğ’°.X j)
  f := ğ’°.f
  memâ‚€ := by
    rw [presieveâ‚€_mem_precoverage_iff]
    refine âŸ¨fun x â†¦ ?_, ğ’°.map_propâŸ©
    obtain âŸ¨y, hyâŸ© := ğ’°.covers x
    use ğ’°.idx x, y

/-- Any `v`-cover `ğ’°` induces a `u`-cover indexed by the points of `X`. -/
@[simps!]
def Cover.ulift (ğ’° : Cover.{v} (precoverage P) X) : Cover.{u} (precoverage P) X where
  Iâ‚€ := X
  X x := ğ’°.X (ğ’°.exists_eq x).choose
  f x := ğ’°.f _
  memâ‚€ := by
    rw [presieveâ‚€_mem_precoverage_iff]
    refine âŸ¨fun x â†¦ ?_, fun i â†¦ ğ’°.map_prop _âŸ©
    use x, (ğ’°.exists_eq x).choose_spec.choose, (ğ’°.exists_eq x).choose_spec.choose_spec

instance : Precoverage.Small.{u} (precoverage P) where
  zeroHypercoverSmall {S} ğ’° := âŸ¨S, Cover.idx ğ’°, (Cover.ulift ğ’°).memâ‚€âŸ©

section category

-- TODO: replace this by `ZeroHypercover.Hom`
/--
A morphism between covers `ğ’° âŸ¶ ğ’±` indicates that `ğ’°` is a refinement of `ğ’±`.
Since covers of schemes are indexed, the definition also involves a map on the
indexing types.
-/
@[ext]
structure Cover.Hom {X : Scheme.{u}} (ğ’° ğ’± : Cover.{v} (precoverage P) X) where
  /-- The map on indexing types associated to a morphism of covers. -/
  idx : ğ’°.Iâ‚€ â†’ ğ’±.Iâ‚€
  /-- The morphism between open subsets associated to a morphism of covers. -/
  app (j : ğ’°.Iâ‚€) : ğ’°.X j âŸ¶ ğ’±.X (idx j)
  app_prop (j : ğ’°.Iâ‚€) : P (app j) := by infer_instance
  w (j : ğ’°.Iâ‚€) : app j â‰« ğ’±.f _ = ğ’°.f _ := by cat_disch

attribute [reassoc (attr := simp)] Cover.Hom.w

/-- The identity morphism in the category of covers of a scheme. -/
def Cover.Hom.id [P.ContainsIdentities] {X : Scheme.{u}} (ğ’° : Cover.{v} (precoverage P) X) :
    ğ’°.Hom ğ’° where
  idx j := j
  app _ := ğŸ™ _
  app_prop _ := P.id_mem _

/-- The composition of two morphisms in the category of covers of a scheme. -/
def Cover.Hom.comp [P.IsStableUnderComposition] {X : Scheme.{u}}
    {ğ’° ğ’± ğ’² : Cover.{v} (precoverage P) X} (f : ğ’°.Hom ğ’±) (g : ğ’±.Hom ğ’²) : ğ’°.Hom ğ’² where
  idx j := g.idx <| f.idx j
  app _ := f.app _ â‰« g.app _
  app_prop _ := P.comp_mem _ _ (f.app_prop _) (g.app_prop _)

instance Cover.category [P.IsMultiplicative] {X : Scheme.{u}} :
    Category (Cover.{v} (precoverage P) X) where
  Hom ğ’° ğ’± := ğ’°.Hom ğ’±
  id := Cover.Hom.id
  comp f g := f.comp g

variable [P.IsMultiplicative]

@[simp]
lemma Cover.id_idx_apply {X : Scheme.{u}} (ğ’° : X.Cover (precoverage P)) (j : ğ’°.Iâ‚€) :
    (ğŸ™ ğ’° : ğ’° âŸ¶ ğ’°).idx j = j := rfl

@[simp]
lemma Cover.id_app {X : Scheme.{u}} (ğ’° : X.Cover (precoverage P)) (j : ğ’°.Iâ‚€) :
    (ğŸ™ ğ’° : ğ’° âŸ¶ ğ’°).app j = ğŸ™ _ := rfl

@[simp]
lemma Cover.comp_idx_apply {X : Scheme.{u}} {ğ’° ğ’± ğ’² : X.Cover (precoverage P)}
    (f : ğ’° âŸ¶ ğ’±) (g : ğ’± âŸ¶ ğ’²) (j : ğ’°.Iâ‚€) :
    (f â‰« g).idx j = g.idx (f.idx j) := rfl

@[simp]
lemma Cover.comp_app {X : Scheme.{u}} {ğ’° ğ’± ğ’² : X.Cover (precoverage P)}
    (f : ğ’° âŸ¶ ğ’±) (g : ğ’± âŸ¶ ğ’²) (j : ğ’°.Iâ‚€) :
    (f â‰« g).app j = f.app j â‰« g.app _ := rfl

end category

end MorphismProperty

end Scheme

end AlgebraicGeometry
