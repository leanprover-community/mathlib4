/-
Copyright (c) 2024 Christian Merten, Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten, Andrew Yang
-/
import Mathlib.AlgebraicGeometry.OpenImmersion
import Mathlib.CategoryTheory.MorphismProperty.Limits

/-!
# Covers of schemes

This file provides the basic API for covers of schemes. A cover of a scheme `X` with respect to
a morphism property `P` is a jointly surjective indexed family of scheme morphisms with
target `X` all satisfying `P`.

## Implementation details

The definition on the pullback of a cover along a morphism depends on results that
are developed later in the import tree. Hence in this file, they have additional assumptions
that will be automatically satisfied in later files. The motivation here is that we already
know that these assumptions are satisfied for open immersions and hence the cover API for open
immersions can be used to deduce these assumptions in the general case.

-/


noncomputable section

open TopologicalSpace CategoryTheory Opposite CategoryTheory.Limits

universe v vâ‚ vâ‚‚ u

namespace AlgebraicGeometry

namespace Scheme

-- TODO: provide API to and from a presieve.
/-- A cover of `X` consists of jointly surjective indexed family of scheme morphisms
with target `X` all satisfying `P`.

This is merely a coverage in the pretopology defined by `P`, and it would be optimal
if we could reuse the existing API about pretopologies, However, the definitions of sieves and
Grothendieck topologies uses `Prop`s, so that the actual open sets and immersions are hard to
obtain. Also, since such a coverage in the pretopology usually contains a proper class of
immersions, it is quite hard to glue them, reason about finite covers, etc.

Note: The `map_prop` field is equipped with a default argument `by infer_instance`. In general
this causes worse error messages, but in practice `P` is mostly defined via `class`.
-/
structure Cover (P : MorphismProperty Scheme.{u}) (S : Scheme.{u}) where
  /-- index set of a cover of a scheme `X` -/
  Iâ‚€ : Type v
  /-- the components of a cover -/
  X (j : Iâ‚€) : Scheme
  /-- the components map to `X` -/
  f (j : Iâ‚€) : X j âŸ¶ S
  /-- given a point of `x : X`, `idx x` is the index of the component which contains `x` -/
  idx (x : S) : Iâ‚€
  /-- the components cover `X` -/
  covers (x : S) : x âˆˆ Set.range (f (idx x)).base
  /-- the component maps satisfy `P` -/
  map_prop (j : Iâ‚€) : P (f j) := by infer_instance

@[deprecated (since := "2025-09-19")]
alias Cover.J := Cover.Iâ‚€

@[deprecated (since := "2025-09-19")]
alias Cover.obj := Cover.X

@[deprecated (since := "2025-09-19")]
alias Cover.map := Cover.f

variable {P : MorphismProperty Scheme.{u}}

variable {X Y Z : Scheme.{u}} (ğ’° : X.Cover P) (f : X âŸ¶ Z) (g : Y âŸ¶ Z)
variable [âˆ€ x, HasPullback (ğ’°.f x â‰« f) g]

theorem Cover.iUnion_range {X : Scheme.{u}} (ğ’° : X.Cover P) :
    â‹ƒ i, Set.range (ğ’°.f i).base = Set.univ := by
  rw [Set.eq_univ_iff_forall]
  intro x
  rw [Set.mem_iUnion]
  exact âŸ¨ğ’°.idx x, ğ’°.covers xâŸ©

lemma Cover.exists_eq (ğ’° : X.Cover P) (x : X) : âˆƒ i y, (ğ’°.f i).base y = x :=
  âŸ¨_, ğ’°.covers xâŸ©

instance Cover.nonempty_of_nonempty [Nonempty X] (ğ’° : X.Cover P) : Nonempty ğ’°.Iâ‚€ :=
  Nonempty.map ğ’°.idx â€¹_â€º

/-- Given a family of schemes with morphisms to `X` satisfying `P` that jointly
cover `X`, `Cover.mkOfCovers` is an associated `P`-cover of `X`. -/
@[simps]
def Cover.mkOfCovers (J : Type*) (obj : J â†’ Scheme.{u}) (map : (j : J) â†’ obj j âŸ¶ X)
    (covers : âˆ€ x, âˆƒ j y, (map j).base y = x)
    (map_prop : âˆ€ j, P (map j) := by infer_instance) : X.Cover P where
  Iâ‚€ := J
  X := obj
  f := map
  idx x := (covers x).choose
  covers x := (covers x).choose_spec
  map_prop := map_prop

/-- Turn a `P`-cover into a `Q`-cover by showing that the components satisfy `Q`. -/
def Cover.changeProp (Q : MorphismProperty Scheme.{u}) (ğ’° : X.Cover P) (h : âˆ€ j, Q (ğ’°.f j)) :
    X.Cover Q where
  Iâ‚€ := ğ’°.Iâ‚€
  X := ğ’°.X
  f := ğ’°.f
  idx := ğ’°.idx
  covers := ğ’°.covers
  map_prop := h

/-- Given a `P`-cover `{ Uáµ¢ }` of `X`, and for each `Uáµ¢` a `P`-cover, we may combine these
covers to form a `P`-cover of `X`. -/
@[simps! Iâ‚€ X f]
def Cover.bind [P.IsStableUnderComposition] (g : âˆ€ x : ğ’°.Iâ‚€, (ğ’°.X x).Cover P) : X.Cover P where
  Iâ‚€ := Î£ i : ğ’°.Iâ‚€, (g i).Iâ‚€
  X x := (g x.1).X x.2
  f x := (g x.1).f x.2 â‰« ğ’°.f x.1
  idx x := âŸ¨_, (g _).idx (ğ’°.covers x).chooseâŸ©
  covers x := by
    let y := (ğ’°.covers x).choose
    have hy : (ğ’°.f (ğ’°.idx x)).base y = x := (ğ’°.covers x).choose_spec
    rcases (g (ğ’°.idx x)).covers y with âŸ¨z, hzâŸ©
    change x âˆˆ Set.range ((g (ğ’°.idx x)).f ((g (ğ’°.idx x)).idx y) â‰« ğ’°.f (ğ’°.idx x)).base
    use z
    simp only [comp_coeBase, TopCat.hom_comp, ContinuousMap.comp_apply]
    rw [hz, hy]
  map_prop _ := P.comp_mem _ _ ((g _).map_prop _) (ğ’°.map_prop _)

/-- An isomorphism `X âŸ¶ Y` is a `P`-cover of `Y`. -/
@[simps Iâ‚€ X f]
def coverOfIsIso [P.ContainsIdentities] [P.RespectsIso] {X Y : Scheme.{u}} (f : X âŸ¶ Y)
    [IsIso f] : Cover.{v} P Y where
  Iâ‚€ := PUnit.{v + 1}
  X _ := X
  f _ := f
  idx _ := PUnit.unit
  covers x := by
    rw [Set.range_eq_univ.mpr]
    all_goals try trivial
    rw [â† TopCat.epi_iff_surjective]
    infer_instance
  map_prop _ := P.of_isIso _

/-- We construct a cover from another, by providing the needed fields and showing that the
provided fields are isomorphic with the original cover. -/
@[simps Iâ‚€ X f]
def Cover.copy [P.RespectsIso] {X : Scheme.{u}} (ğ’° : X.Cover P)
    (J : Type*) (obj : J â†’ Scheme)
    (map : âˆ€ i, obj i âŸ¶ X) (eâ‚ : J â‰ƒ ğ’°.Iâ‚€) (eâ‚‚ : âˆ€ i, obj i â‰… ğ’°.X (eâ‚ i))
    (h : âˆ€ i, map i = (eâ‚‚ i).hom â‰« ğ’°.f (eâ‚ i)) : X.Cover P :=
  { Iâ‚€ := J, X := obj, f := map
    idx := fun x â†¦ eâ‚.symm (ğ’°.idx x)
    covers := fun x â†¦ by
      rw [h, Scheme.comp_base, TopCat.coe_comp, Set.range_comp, Set.range_eq_univ.mpr,
        Set.image_univ, eâ‚.rightInverse_symm]
      Â· exact ğ’°.covers x
      Â· rw [â† TopCat.epi_iff_surjective]; infer_instance
    map_prop := fun j â†¦ by
      rw [h, P.cancel_left_of_respectsIso]
      exact ğ’°.map_prop (eâ‚ j) }

/-- The pushforward of a cover along an isomorphism. -/
@[simps! Iâ‚€ X f]
def Cover.pushforwardIso [P.RespectsIso] [P.ContainsIdentities] [P.IsStableUnderComposition]
    {X Y : Scheme.{u}} (ğ’° : Cover.{v} P X) (f : X âŸ¶ Y) [IsIso f] :
    Cover.{v} P Y :=
  ((coverOfIsIso.{v, u} f).bind fun _ => ğ’°).copy ğ’°.Iâ‚€ _ _
    ((Equiv.punitProd _).symm.trans (Equiv.sigmaEquivProd PUnit ğ’°.Iâ‚€).symm) (fun _ => Iso.refl _)
    fun _ => (Category.id_comp _).symm

/-- Adding map satisfying `P` into a cover gives another cover. -/
@[simps]
def Cover.add {X Y : Scheme.{u}} (ğ’° : X.Cover P) (f : Y âŸ¶ X) (hf : P f := by infer_instance) :
    X.Cover P where
  Iâ‚€ := Option ğ’°.Iâ‚€
  X i := Option.rec Y ğ’°.X i
  f i := Option.rec f ğ’°.f i
  idx x := some (ğ’°.idx x)
  covers := ğ’°.covers
  map_prop j := by
    obtain âŸ¨_ | _âŸ© := j
    Â· exact hf
    Â· exact ğ’°.map_prop _

/-- A morphism property of schemes is said to preserve joint surjectivity, if
for any pair of morphisms `f : X âŸ¶ S` and `g : Y âŸ¶ S` where `g` satisfies `P`,
any pair of points `x : X` and `y : Y` with `f x = g y` can be lifted to a point
of `X Ã—[S] Y`.

In later files, this will be automatic, since this holds for any morphism `g`
(see `AlgebraicGeometry.Scheme.isJointlySurjectivePreserving`). But at
this early stage in the import tree, we only know it for open immersions. -/
class IsJointlySurjectivePreserving (P : MorphismProperty Scheme.{u}) where
  exists_preimage_fst_triplet_of_prop {X Y S : Scheme.{u}} {f : X âŸ¶ S} {g : Y âŸ¶ S} [HasPullback f g]
    (hg : P g) (x : X) (y : Y) (h : f.base x = g.base y) :
    âˆƒ a : â†‘(pullback f g), (pullback.fst f g).base a = x

lemma IsJointlySurjectivePreserving.exists_preimage_snd_triplet_of_prop
    [IsJointlySurjectivePreserving P] {X Y S : Scheme.{u}} {f : X âŸ¶ S} {g : Y âŸ¶ S} [HasPullback f g]
    (hf : P f) (x : X) (y : Y) (h : f.base x = g.base y) :
    âˆƒ a : â†‘(pullback f g), (pullback.snd f g).base a = y := by
  let iso := pullbackSymmetry f g
  haveI : HasPullback g f := hasPullback_symmetry f g
  obtain âŸ¨a, haâŸ© := exists_preimage_fst_triplet_of_prop hf y x h.symm
  use (pullbackSymmetry f g).inv.base a
  rwa [â† Scheme.comp_base_apply, pullbackSymmetry_inv_comp_snd]

instance : IsJointlySurjectivePreserving @IsOpenImmersion where
  exists_preimage_fst_triplet_of_prop {X Y S f g} _ hg x y h := by
    rw [â† show _ = (pullback.fst _ _ : pullback f g âŸ¶ _).base from
        PreservesPullback.iso_hom_fst Scheme.forgetToTop f g]
    have : x âˆˆ Set.range (pullback.fst f.base g.base) := by
      rw [TopCat.pullback_fst_range f.base g.base]
      use y
    obtain âŸ¨a, haâŸ© := this
    use (PreservesPullback.iso forgetToTop f g).inv a
    rwa [â† TopCat.comp_app, Iso.inv_hom_id_assoc]

/-- Given a cover on `X`, we may pull them back along a morphism `W âŸ¶ X` to obtain
a cover of `W`.

Note that this requires the (unnecessary) assumptions that the pullback exists and that `P`
preserves joint surjectivity. This is needed, because we don't know these in general at this
stage of the import tree, but this API is used in the case of `P = IsOpenImmersion` to
obtain these results in the general case. -/
@[simps]
def Cover.pullbackCover [P.IsStableUnderBaseChange] [IsJointlySurjectivePreserving P]
    {X W : Scheme.{u}} (ğ’° : X.Cover P) (f : W âŸ¶ X) [âˆ€ x, HasPullback f (ğ’°.f x)] : W.Cover P where
  Iâ‚€ := ğ’°.Iâ‚€
  X x := pullback f (ğ’°.f x)
  f _ := pullback.fst _ _
  idx x := ğ’°.idx (f.base x)
  covers x := by
    obtain âŸ¨y, hyâŸ© := ğ’°.covers (f.base x)
    exact IsJointlySurjectivePreserving.exists_preimage_fst_triplet_of_prop
      (ğ’°.map_prop _) x y hy.symm
  map_prop j := P.pullback_fst _ _ (ğ’°.map_prop j)

/-- The family of morphisms from the pullback cover to the original cover. -/
def Cover.pullbackHom [P.IsStableUnderBaseChange] [IsJointlySurjectivePreserving P]
    {X W : Scheme.{u}} (ğ’° : X.Cover P)
    (f : W âŸ¶ X) (i) [âˆ€ x, HasPullback f (ğ’°.f x)] :
    (ğ’°.pullbackCover f).X i âŸ¶ ğ’°.X i :=
  pullback.snd f (ğ’°.f i)

@[reassoc (attr := simp)]
lemma Cover.pullbackHom_map [P.IsStableUnderBaseChange] [IsJointlySurjectivePreserving P]
    {X W : Scheme.{u}} (ğ’° : X.Cover P) (f : W âŸ¶ X) [âˆ€ (x : ğ’°.Iâ‚€), HasPullback f (ğ’°.f x)] (i) :
    ğ’°.pullbackHom f i â‰« ğ’°.f i = (ğ’°.pullbackCover f).f i â‰« f := pullback.condition.symm

/-- Given a cover on `X`, we may pull them back along a morphism `f : W âŸ¶ X` to obtain
a cover of `W`. This is similar to `Scheme.Cover.pullbackCover`, but here we
take `pullback (ğ’°.f x) f` instead of `pullback f (ğ’°.f x)`. -/
@[simps]
def Cover.pullbackCover' [P.IsStableUnderBaseChange] [IsJointlySurjectivePreserving P]
    {X W : Scheme.{u}} (ğ’° : X.Cover P) (f : W âŸ¶ X)
    [âˆ€ x, HasPullback (ğ’°.f x) f] :
    W.Cover P where
  Iâ‚€ := ğ’°.Iâ‚€
  X x := pullback (ğ’°.f x) f
  f _ := pullback.snd _ _
  idx x := ğ’°.idx (f.base x)
  covers x := by
    obtain âŸ¨y, hyâŸ© := ğ’°.covers (f.base x)
    exact IsJointlySurjectivePreserving.exists_preimage_snd_triplet_of_prop
      (ğ’°.map_prop _) y x hy
  map_prop j := P.pullback_snd _ _ (ğ’°.map_prop j)

/-- Given covers `{ Uáµ¢ }` and `{ Uâ±¼ }`, we may form the cover `{ Uáµ¢ Ã—[X] Uâ±¼ }`. -/
def Cover.inter [P.IsStableUnderBaseChange] [P.IsStableUnderComposition]
    [IsJointlySurjectivePreserving P]
    {X : Scheme.{u}} (ğ’°â‚ : Scheme.Cover.{vâ‚} P X)
    (ğ’°â‚‚ : Scheme.Cover.{vâ‚‚} P X)
    [âˆ€ (i : ğ’°â‚.Iâ‚€) (j : ğ’°â‚‚.Iâ‚€), HasPullback (ğ’°â‚.f i) (ğ’°â‚‚.f j)] : X.Cover P where
  Iâ‚€ := ğ’°â‚.Iâ‚€ Ã— ğ’°â‚‚.Iâ‚€
  X ij := pullback (ğ’°â‚.f ij.1) (ğ’°â‚‚.f ij.2)
  f ij := pullback.fst _ _ â‰« ğ’°â‚.f ij.1
  idx x := âŸ¨ğ’°â‚.idx x, ğ’°â‚‚.idx xâŸ©
  covers x := by
    simp only [comp_coeBase, TopCat.coe_comp, Set.mem_range, Function.comp_apply]
    obtain âŸ¨yâ‚, hyâ‚âŸ© := ğ’°â‚.covers x
    obtain âŸ¨yâ‚‚, hyâ‚‚âŸ© := ğ’°â‚‚.covers x
    obtain âŸ¨z, hzâŸ© := IsJointlySurjectivePreserving.exists_preimage_fst_triplet_of_prop
      (ğ’°â‚‚.map_prop _) yâ‚ yâ‚‚ (by rw [hyâ‚, hyâ‚‚])
    use z
    rw [hz, hyâ‚]
  map_prop ij := P.comp_mem _ _ (P.pullback_fst _ _ (ğ’°â‚‚.map_prop ij.2)) (ğ’°â‚.map_prop ij.1)

/--
An affine cover of `X` consists of a jointly surjective family of maps into `X` from
spectra of rings.

Note: The `map_prop` field is equipped with a default argument `by infer_instance`. In general
this causes worse error messages, but in practice `P` is mostly defined via `class`.
-/
structure AffineCover (P : MorphismProperty Scheme.{u}) (S : Scheme.{u}) where
  /-- index set of an affine cover of a scheme `S` -/
  Iâ‚€ : Type v
  /-- the ring associated to a component of an affine cover -/
  X (j : Iâ‚€) : CommRingCat.{u}
  /-- the components map to `S` -/
  f (j : Iâ‚€) : Spec (X j) âŸ¶ S
  /-- given a point of `x : S`, `idx x` is the index of the component which contains `x` -/
  idx (x : S) : Iâ‚€
  /-- the components cover `S` -/
  covers (x : S) : x âˆˆ Set.range (f (idx x)).base
  /-- the component maps satisfy `P` -/
  map_prop (j : Iâ‚€) : P (f j) := by infer_instance

@[deprecated (since := "2025-09-19")]
alias AffineCover.J := AffineCover.Iâ‚€

@[deprecated (since := "2025-09-19")]
alias AffineCover.obj := AffineCover.X

@[deprecated (since := "2025-09-19")]
alias AffineCover.map := AffineCover.f

/-- The cover associated to an affine cover. -/
@[simps]
def AffineCover.cover {S : Scheme.{u}} (ğ’° : S.AffineCover P) : S.Cover P where
  Iâ‚€ := ğ’°.Iâ‚€
  X j := Spec (ğ’°.X j)
  f := ğ’°.f
  idx := ğ’°.idx
  covers := ğ’°.covers
  map_prop := ğ’°.map_prop

/-- Replace the index type of a cover by an equivalent one. -/
@[simps]
def Cover.reindex (ğ’° : Cover.{v} P X) {Î¹ : Type*} (e : Î¹ â‰ƒ ğ’°.Iâ‚€) : Cover P X where
  Iâ‚€ := Î¹
  X := ğ’°.X âˆ˜ e
  f i := ğ’°.f (e i)
  idx := e.symm âˆ˜ ğ’°.idx
  covers x := by
    convert ğ’°.covers _
    dsimp only [Function.comp_apply]
    rw [Equiv.apply_symm_apply]
  map_prop i := ğ’°.map_prop _

/-- Any `v`-cover `ğ’°` induces a `u`-cover indexed by the points of `X`. -/
@[simps!]
def Cover.ulift (ğ’° : Cover.{v} P X) : Cover.{u} P X where
  Iâ‚€ := X
  X x := ğ’°.X (ğ’°.idx x)
  f x := ğ’°.f (ğ’°.idx x)
  idx := id
  covers := ğ’°.covers
  map_prop _ := ğ’°.map_prop _

section category

/--
A morphism between covers `ğ’° âŸ¶ ğ’±` indicates that `ğ’°` is a refinement of `ğ’±`.
Since covers of schemes are indexed, the definition also involves a map on the
indexing types.
-/
@[ext]
structure Cover.Hom {X : Scheme.{u}} (ğ’° ğ’± : Cover.{v} P X) where
  /-- The map on indexing types associated to a morphism of covers. -/
  idx : ğ’°.Iâ‚€ â†’ ğ’±.Iâ‚€
  /-- The morphism between open subsets associated to a morphism of covers. -/
  app (j : ğ’°.Iâ‚€) : ğ’°.X j âŸ¶ ğ’±.X (idx j)
  app_prop (j : ğ’°.Iâ‚€) : P (app j) := by infer_instance
  w (j : ğ’°.Iâ‚€) : app j â‰« ğ’±.f _ = ğ’°.f _ := by cat_disch

attribute [reassoc (attr := simp)] Cover.Hom.w

/-- The identity morphism in the category of covers of a scheme. -/
def Cover.Hom.id [P.ContainsIdentities] {X : Scheme.{u}} (ğ’° : Cover.{v} P X) : ğ’°.Hom ğ’° where
  idx j := j
  app _ := ğŸ™ _
  app_prop _ := P.id_mem _

/-- The composition of two morphisms in the category of covers of a scheme. -/
def Cover.Hom.comp [P.IsStableUnderComposition] {X : Scheme.{u}} {ğ’° ğ’± ğ’² : Cover.{v} P X}
    (f : ğ’°.Hom ğ’±) (g : ğ’±.Hom ğ’²) : ğ’°.Hom ğ’² where
  idx j := g.idx <| f.idx j
  app _ := f.app _ â‰« g.app _
  app_prop _ := P.comp_mem _ _ (f.app_prop _) (g.app_prop _)

instance Cover.category [P.IsMultiplicative] {X : Scheme.{u}} : Category (Cover.{v} P X) where
  Hom ğ’° ğ’± := ğ’°.Hom ğ’±
  id := Cover.Hom.id
  comp f g := f.comp g

variable [P.IsMultiplicative]

@[simp]
lemma Cover.id_idx_apply {X : Scheme.{u}} (ğ’° : X.Cover P) (j : ğ’°.Iâ‚€) :
    (ğŸ™ ğ’° : ğ’° âŸ¶ ğ’°).idx j = j := rfl

@[simp]
lemma Cover.id_app {X : Scheme.{u}} (ğ’° : X.Cover P) (j : ğ’°.Iâ‚€) :
    (ğŸ™ ğ’° : ğ’° âŸ¶ ğ’°).app j = ğŸ™ _ := rfl

@[simp]
lemma Cover.comp_idx_apply {X : Scheme.{u}} {ğ’° ğ’± ğ’² : X.Cover P}
    (f : ğ’° âŸ¶ ğ’±) (g : ğ’± âŸ¶ ğ’²) (j : ğ’°.Iâ‚€) :
    (f â‰« g).idx j = g.idx (f.idx j) := rfl

@[simp]
lemma Cover.comp_app {X : Scheme.{u}} {ğ’° ğ’± ğ’² : X.Cover P}
    (f : ğ’° âŸ¶ ğ’±) (g : ğ’± âŸ¶ ğ’²) (j : ğ’°.Iâ‚€) :
    (f â‰« g).app j = f.app j â‰« g.app _ := rfl

end category

end Scheme

end AlgebraicGeometry
