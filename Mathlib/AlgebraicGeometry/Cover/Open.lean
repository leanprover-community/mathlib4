/-
Copyright (c) 2024 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.OpenImmersion

/-!
# Open covers of schemes

This file provides the basic API for open covers of schemes.

## Main definition
- `AlgebraicGeometry.Scheme.OpenCover`: The type of open covers of a scheme `X`,
  consisting of a family of open immersions into `X`,
  and for each `x : X` an open immersion (indexed by `f x`) that covers `x`.
- `AlgebraicGeometry.Scheme.affineCover`: `X.affineCover` is a choice of an affine cover of `X`.
- `AlgebraicGeometry.Scheme.AffineOpenCover`: The type of affine open covers of a scheme `X`.
-/

set_option linter.uppercaseLean3 false

noncomputable section

open TopologicalSpace CategoryTheory Opposite CategoryTheory.Limits

universe v vâ‚ vâ‚‚ u

namespace AlgebraicGeometry

namespace Scheme

-- TODO: provide API to and from a presieve.
/-- An open cover of `X` consists of a family of open immersions into `X`,
and for each `x : X` an open immersion (indexed by `f x`) that covers `x`.

This is merely a coverage in the Zariski pretopology, and it would be optimal
if we could reuse the existing API about pretopologies, However, the definitions of sieves and
grothendieck topologies uses `Prop`s, so that the actual open sets and immersions are hard to
obtain. Also, since such a coverage in the pretopology usually contains a proper class of
immersions, it is quite hard to glue them, reason about finite covers, etc.
-/
structure OpenCover (X : Scheme.{u}) where
  /-- index set of an open cover of a scheme `X` -/
  J : Type v
  /-- the subschemes of an open cover -/
  obj : J â†’ Scheme
  /-- the embedding of subschemes to `X` -/
  map : âˆ€ j : J, obj j âŸ¶ X
  /-- given a point of `x : X`, `f x` is the index of the subscheme which contains `x`  -/
  f : X.carrier â†’ J
  /-- the subschemes covers `X` -/
  Covers : âˆ€ x, x âˆˆ Set.range (map (f x)).1.base
  /-- the embedding of subschemes are open immersions -/
  IsOpen : âˆ€ x, IsOpenImmersion (map x) := by infer_instance
#align algebraic_geometry.Scheme.open_cover AlgebraicGeometry.Scheme.OpenCover

attribute [instance] OpenCover.IsOpen

variable {X Y Z : Scheme.{u}} (ğ’° : OpenCover X) (f : X âŸ¶ Z) (g : Y âŸ¶ Z)
variable [âˆ€ x, HasPullback (ğ’°.map x â‰« f) g]

/-- The affine cover of a scheme. -/
def affineCover (X : Scheme.{u}) : OpenCover X where
  J := X.carrier
  obj x := Spec.obj <| Opposite.op (X.local_affine x).choose_spec.choose
  map x :=
    ((X.local_affine x).choose_spec.choose_spec.some.inv â‰« X.toLocallyRingedSpace.ofRestrict _ : _)
  f x := x
  Covers := by
    intro x
    erw [TopCat.coe_comp] -- now `erw` after #13170
    rw [Set.range_comp, Set.range_iff_surjective.mpr, Set.image_univ]
    Â· erw [Subtype.range_coe_subtype]
      exact (X.local_affine x).choose.2
    erw [â† TopCat.epi_iff_surjective] -- now `erw` after #13170
    change Epi ((SheafedSpace.forget _).map (LocallyRingedSpace.forgetToSheafedSpace.map _))
    infer_instance
#align algebraic_geometry.Scheme.affine_cover AlgebraicGeometry.Scheme.affineCover

instance : Inhabited X.OpenCover :=
  âŸ¨X.affineCoverâŸ©

theorem OpenCover.iUnion_range {X : Scheme.{u}} (ğ’° : X.OpenCover) :
    â‹ƒ i, Set.range (ğ’°.map i).1.base = Set.univ := by
  rw [Set.eq_univ_iff_forall]
  intro x
  rw [Set.mem_iUnion]
  exact âŸ¨ğ’°.f x, ğ’°.Covers xâŸ©
#align algebraic_geometry.Scheme.open_cover.Union_range AlgebraicGeometry.Scheme.OpenCover.iUnion_range

theorem OpenCover.iSup_opensRange {X : Scheme.{u}} (ğ’° : X.OpenCover) :
    â¨† i, Scheme.Hom.opensRange (ğ’°.map i) = âŠ¤ :=
  Opens.ext <| by rw [Opens.coe_iSup]; exact ğ’°.iUnion_range
#align algebraic_geometry.Scheme.open_cover.supr_opens_range AlgebraicGeometry.Scheme.OpenCover.iSup_opensRange

/-- Given an open cover `{ Uáµ¢ }` of `X`, and for each `Uáµ¢` an open cover, we may combine these
open covers to form an open cover of `X`.  -/
@[simps! J obj map]
def OpenCover.bind (f : âˆ€ x : ğ’°.J, OpenCover (ğ’°.obj x)) : OpenCover X where
  J := Î£ i : ğ’°.J, (f i).J
  obj x := (f x.1).obj x.2
  map x := (f x.1).map x.2 â‰« ğ’°.map x.1
  f x := âŸ¨_, (f _).f (ğ’°.Covers x).chooseâŸ©
  Covers x := by
    let y := (ğ’°.Covers x).choose
    have hy : (ğ’°.map (ğ’°.f x)).val.base y = x := (ğ’°.Covers x).choose_spec
    rcases (f (ğ’°.f x)).Covers y with âŸ¨z, hzâŸ©
    change x âˆˆ Set.range ((f (ğ’°.f x)).map ((f (ğ’°.f x)).f y) â‰« ğ’°.map (ğ’°.f x)).1.base
    use z
    erw [comp_apply]
    erw [hz, hy] -- now `erw` after #13170
  -- Porting note: weirdly, even though no input is needed, `inferInstance` does not work
  -- `PresheafedSpace.IsOpenImmersion.comp` is marked as `instance`
  IsOpen x := PresheafedSpace.IsOpenImmersion.comp _ _
#align algebraic_geometry.Scheme.open_cover.bind AlgebraicGeometry.Scheme.OpenCover.bind

/-- An isomorphism `X âŸ¶ Y` is an open cover of `Y`. -/
@[simps J obj map]
def openCoverOfIsIso {X Y : Scheme.{u}} (f : X âŸ¶ Y) [IsIso f] : OpenCover.{v} Y where
  J := PUnit.{v + 1}
  obj _ := X
  map _ := f
  f _ := PUnit.unit
  Covers x := by
    rw [Set.range_iff_surjective.mpr]
    all_goals try trivial
    rw [â† TopCat.epi_iff_surjective]
    infer_instance
#align algebraic_geometry.Scheme.open_cover_of_is_iso AlgebraicGeometry.Scheme.openCoverOfIsIso

/-- We construct an open cover from another, by providing the needed fields and showing that the
provided fields are isomorphic with the original open cover. -/
@[simps J obj map]
def OpenCover.copy {X : Scheme.{u}} (ğ’° : OpenCover X) (J : Type*) (obj : J â†’ Scheme)
    (map : âˆ€ i, obj i âŸ¶ X) (eâ‚ : J â‰ƒ ğ’°.J) (eâ‚‚ : âˆ€ i, obj i â‰… ğ’°.obj (eâ‚ i))
    (eâ‚‚ : âˆ€ i, map i = (eâ‚‚ i).hom â‰« ğ’°.map (eâ‚ i)) : OpenCover X :=
  { J, obj, map
    f := fun x => eâ‚.symm (ğ’°.f x)
    Covers := fun x => by
      rw [eâ‚‚, Scheme.comp_val_base, TopCat.coe_comp, Set.range_comp, Set.range_iff_surjective.mpr,
        Set.image_univ, eâ‚.rightInverse_symm]
      Â· exact ğ’°.Covers x
      Â· erw [â† TopCat.epi_iff_surjective]; infer_instance -- now `erw` after #13170
    -- Porting note: weirdly, even though no input is needed, `inferInstance` does not work
    -- `PresheafedSpace.IsOpenImmersion.comp` is marked as `instance`
    IsOpen := fun i => by rw [eâ‚‚]; exact PresheafedSpace.IsOpenImmersion.comp _ _ }
#align algebraic_geometry.Scheme.open_cover.copy AlgebraicGeometry.Scheme.OpenCover.copy

-- Porting note: need more hint on universe level
/-- The pushforward of an open cover along an isomorphism. -/
@[simps! J obj map]
def OpenCover.pushforwardIso {X Y : Scheme.{u}} (ğ’° : OpenCover.{v} X) (f : X âŸ¶ Y) [IsIso f] :
    OpenCover.{v} Y :=
  ((openCoverOfIsIso.{v, u} f).bind fun _ => ğ’°).copy ğ’°.J _ _
    ((Equiv.punitProd _).symm.trans (Equiv.sigmaEquivProd PUnit ğ’°.J).symm) (fun _ => Iso.refl _)
    fun _ => (Category.id_comp _).symm
#align algebraic_geometry.Scheme.open_cover.pushforward_iso AlgebraicGeometry.Scheme.OpenCover.pushforwardIso

/-- Adding an open immersion into an open cover gives another open cover. -/
@[simps]
def OpenCover.add {X Y : Scheme.{u}} (ğ’° : X.OpenCover) (f : Y âŸ¶ X) [IsOpenImmersion f] :
    X.OpenCover where
  J := Option ğ’°.J
  obj i := Option.rec Y ğ’°.obj i
  map i := Option.rec f ğ’°.map i
  f x := some (ğ’°.f x)
  Covers := ğ’°.Covers
  IsOpen := by rintro (_ | _) <;> dsimp <;> infer_instance
#align algebraic_geometry.Scheme.open_cover.add AlgebraicGeometry.Scheme.OpenCover.add

/-- Given an open cover on `X`, we may pull them back along a morphism `W âŸ¶ X` to obtain
an open cover of `W`. -/
@[simps]
def OpenCover.pullbackCover {X W : Scheme.{u}} (ğ’° : X.OpenCover) (f : W âŸ¶ X) :
    W.OpenCover where
  J := ğ’°.J
  obj x := pullback f (ğ’°.map x)
  map x := pullback.fst
  f x := ğ’°.f (f.1.base x)
  Covers x := by
    rw [â†
      show _ = (pullback.fst : pullback f (ğ’°.map (ğ’°.f (f.1.base x))) âŸ¶ _).1.base from
        PreservesPullback.iso_hom_fst Scheme.forgetToTop f (ğ’°.map (ğ’°.f (f.1.base x)))]
    -- Porting note: `rw` to `erw` on this single lemma
    rw [TopCat.coe_comp, Set.range_comp, Set.range_iff_surjective.mpr, Set.image_univ,
      TopCat.pullback_fst_range]
    Â· obtain âŸ¨y, hâŸ© := ğ’°.Covers (f.1.base x)
      exact âŸ¨y, h.symmâŸ©
    Â· rw [â† TopCat.epi_iff_surjective]; infer_instance
#align algebraic_geometry.Scheme.open_cover.pullback_cover AlgebraicGeometry.Scheme.OpenCover.pullbackCover

/-- Given an open cover on `X`, we may pull them back along a morphism `f : W âŸ¶ X` to obtain
an open cover of `W`. This is similar to `Scheme.OpenCover.pullbackCover`, but here we
take `pullback (ğ’°.map x) f` instead of `pullback f (ğ’°.map x)`. -/
@[simps]
def OpenCover.pullbackCover' {X W : Scheme.{u}} (ğ’° : X.OpenCover) (f : W âŸ¶ X) :
    W.OpenCover where
  J := ğ’°.J
  obj x := pullback (ğ’°.map x) f
  map x := pullback.snd
  f x := ğ’°.f (f.1.base x)
  Covers x := by
    rw [â†
      show _ = (pullback.snd : pullback (ğ’°.map (ğ’°.f (f.1.base x))) f âŸ¶ _).1.base from
        PreservesPullback.iso_hom_snd Scheme.forgetToTop (ğ’°.map (ğ’°.f (f.1.base x))) f]
    -- Porting note: `rw` to `erw` on this single lemma
    rw [TopCat.coe_comp, Set.range_comp, Set.range_iff_surjective.mpr, Set.image_univ,
      TopCat.pullback_snd_range]
    Â· obtain âŸ¨y, hâŸ© := ğ’°.Covers (f.1.base x)
      exact âŸ¨y, hâŸ©
    Â· rw [â† TopCat.epi_iff_surjective]; infer_instance

/-- Every open cover of a quasi-compact scheme can be refined into a finite subcover.
-/
@[simps! obj map]
def OpenCover.finiteSubcover {X : Scheme.{u}} (ğ’° : OpenCover X) [H : CompactSpace X] :
    OpenCover X := by
  have :=
    @CompactSpace.elim_nhds_subcover _ _ H (fun x : X => Set.range (ğ’°.map (ğ’°.f x)).1.base)
      fun x => (IsOpenImmersion.isOpen_range (ğ’°.map (ğ’°.f x))).mem_nhds (ğ’°.Covers x)
  let t := this.choose
  have h : âˆ€ x : X, âˆƒ y : t, x âˆˆ Set.range (ğ’°.map (ğ’°.f y)).1.base := by
    intro x
    have h' : x âˆˆ (âŠ¤ : Set X) := trivial
    rw [â† Classical.choose_spec this, Set.mem_iUnion] at h'
    rcases h' with âŸ¨y, _, âŸ¨hy, rflâŸ©, hy'âŸ©
    exact âŸ¨âŸ¨y, hyâŸ©, hy'âŸ©
  exact
    { J := t
      obj := fun x => ğ’°.obj (ğ’°.f x.1)
      map := fun x => ğ’°.map (ğ’°.f x.1)
      f := fun x => (h x).choose
      Covers := fun x => (h x).choose_spec }
#align algebraic_geometry.Scheme.open_cover.finite_subcover AlgebraicGeometry.Scheme.OpenCover.finiteSubcover

instance [H : CompactSpace X] : Fintype ğ’°.finiteSubcover.J := by
  delta OpenCover.finiteSubcover; infer_instance

theorem OpenCover.compactSpace {X : Scheme.{u}} (ğ’° : X.OpenCover) [Finite ğ’°.J]
    [H : âˆ€ i, CompactSpace (ğ’°.obj i)] : CompactSpace X := by
  cases nonempty_fintype ğ’°.J
  rw [â† isCompact_univ_iff, â† ğ’°.iUnion_range]
  apply isCompact_iUnion
  intro i
  rw [isCompact_iff_compactSpace]
  exact
    @Homeomorph.compactSpace _ _ _ _ (H i)
      (TopCat.homeoOfIso
        (asIso
          (IsOpenImmersion.isoOfRangeEq (ğ’°.map i)
                  (X.ofRestrict (Opens.openEmbedding âŸ¨_, (ğ’°.IsOpen i).base_open.isOpen_rangeâŸ©))
                  Subtype.range_coe.symm).hom.1.base))
#align algebraic_geometry.Scheme.open_cover.compact_space AlgebraicGeometry.Scheme.OpenCover.compactSpace

/-- Given open covers `{ Uáµ¢ }` and `{ Uâ±¼ }`, we may form the open cover `{ Uáµ¢ âˆ© Uâ±¼ }`. -/
def OpenCover.inter {X : Scheme.{u}} (ğ’°â‚ : Scheme.OpenCover.{vâ‚} X)
    (ğ’°â‚‚ : Scheme.OpenCover.{vâ‚‚} X) : X.OpenCover where
  J := ğ’°â‚.J Ã— ğ’°â‚‚.J
  obj ij := pullback (ğ’°â‚.map ij.1) (ğ’°â‚‚.map ij.2)
  map ij := pullback.fst â‰« ğ’°â‚.map ij.1
  f x := âŸ¨ğ’°â‚.f x, ğ’°â‚‚.f xâŸ©
  Covers x := by
    rw [IsOpenImmersion.range_pullback_to_base_of_left]
    exact âŸ¨ğ’°â‚.Covers x, ğ’°â‚‚.Covers xâŸ©
  -- Porting note: was automatic
  IsOpen x := PresheafedSpace.IsOpenImmersion.comp (hf := inferInstance) (hg := (ğ’°â‚.IsOpen _))
#align algebraic_geometry.Scheme.open_cover.inter AlgebraicGeometry.Scheme.OpenCover.inter

/-- If `U` is a family of open sets that covers `X`, then `X.restrict U` forms an `X.open_cover`. -/
@[simps! J obj map]
def openCoverOfSuprEqTop {s : Type*} (X : Scheme.{u}) (U : s â†’ Opens X)
    (hU : â¨† i, U i = âŠ¤) : X.OpenCover where
  J := s
  obj i := X.restrict (U i).openEmbedding
  map i := X.ofRestrict (U i).openEmbedding
  f x :=
    haveI : x âˆˆ â¨† i, U i := hU.symm â–¸ show x âˆˆ (âŠ¤ : Opens X) by trivial
    (Opens.mem_iSup.mp this).choose
  Covers x := by
    erw [Subtype.range_coe]
    have : x âˆˆ â¨† i, U i := hU.symm â–¸ show x âˆˆ (âŠ¤ : Opens X) by trivial
    exact (Opens.mem_iSup.mp this).choose_spec
#align algebraic_geometry.Scheme.open_cover_of_supr_eq_top AlgebraicGeometry.Scheme.openCoverOfSuprEqTop

/--
An affine open cover of `X` consists of a family of open immersions into `X` from
spectra of rings.
-/
structure AffineOpenCover (X : Scheme.{u}) where
  /-- index set of an affine open cover of a scheme `X` -/
  J : Type v
  /-- the ring associated to a component of an affine open cover -/
  obj : J â†’ CommRingCat.{u}
  /-- the embedding of subschemes to `X` -/
  map : âˆ€ j : J, Spec.obj (.op <| obj j) âŸ¶ X
  /-- given a point of `x : X`, `f x` is the index of the subscheme which contains `x`  -/
  f : X.carrier â†’ J
  /-- the subschemes covers `X` -/
  Covers : âˆ€ x, x âˆˆ Set.range (map (f x)).1.base
  /-- the embedding of subschemes are open immersions -/
  IsOpen : âˆ€ x, IsOpenImmersion (map x) := by infer_instance

namespace AffineOpenCover

attribute [instance] AffineOpenCover.IsOpen

/-- The open cover associated to an affine open cover. -/
@[simps]
def openCover {X : Scheme.{u}} (ğ“¤ : X.AffineOpenCover) : X.OpenCover where
  J := ğ“¤.J
  map := ğ“¤.map
  f := ğ“¤.f
  Covers := ğ“¤.Covers

end AffineOpenCover

/-- A choice of an affine open cover of a scheme. -/
def affineOpenCover (X : Scheme.{u}) : X.AffineOpenCover where
  J := X.affineCover.J
  map := X.affineCover.map
  f := X.affineCover.f
  Covers := X.affineCover.Covers

@[simp]
lemma openCover_affineOpenCover (X : Scheme.{u}) : X.affineOpenCover.openCover = X.affineCover :=
  rfl

/-- Given any open cover `ğ“¤`, this is an affine open cover which refines it.
The morphism in the category of open covers which proves that this is indeed a refinement, see
`AlgebraicGeometry.Scheme.OpenCover.fromAffineRefinement`.
-/
def OpenCover.affineRefinement {X : Scheme.{u}} (ğ“¤ : X.OpenCover) : X.AffineOpenCover where
  J := (ğ“¤.bind fun j => (ğ“¤.obj j).affineCover).J
  map := (ğ“¤.bind fun j => (ğ“¤.obj j).affineCover).map
  f := (ğ“¤.bind fun j => (ğ“¤.obj j).affineCover).f
  Covers := (ğ“¤.bind fun j => (ğ“¤.obj j).affineCover).Covers

section category

/--
A morphism between open covers `ğ“¤ âŸ¶ ğ“¥` indicates that `ğ“¤` is a refinement of `ğ“¥`.
Since open covers of schemes are indexed, the definition also involves a map on the
indexing types.
-/
structure OpenCover.Hom {X : Scheme.{u}} (ğ“¤ ğ“¥ : OpenCover.{v} X) where
  /-- The map on indexing types associated to a morphism of open covers. -/
  idx : ğ“¤.J â†’ ğ“¥.J
  /-- The morphism between open subsets associated to a morphism of open covers. -/
  app (j : ğ“¤.J) : ğ“¤.obj j âŸ¶ ğ“¥.obj (idx j)
  isOpen (j : ğ“¤.J) : IsOpenImmersion (app j) := by infer_instance
  w (j : ğ“¤.J) : app j â‰« ğ“¥.map _ = ğ“¤.map _ := by aesop_cat

attribute [reassoc (attr := simp)] OpenCover.Hom.w
attribute [instance] OpenCover.Hom.isOpen

/-- The identity morphism in the category of open covers of a scheme. -/
def OpenCover.Hom.id {X : Scheme.{u}} (ğ“¤ : OpenCover.{v} X) : ğ“¤.Hom ğ“¤ where
  idx j := j
  app j := ğŸ™ _

/-- The composition of two morphisms in the category of open covers of a scheme. -/
def OpenCover.Hom.comp {X : Scheme.{u}} {ğ“¤ ğ“¥ ğ“¦ : OpenCover.{v} X}
    (f : ğ“¤.Hom ğ“¥) (g : ğ“¥.Hom ğ“¦) : ğ“¤.Hom ğ“¦ where
  idx j := g.idx <| f.idx j
  app j := f.app _ â‰« g.app _

instance OpenCover.category {X : Scheme.{u}} : Category (OpenCover.{v} X) where
  Hom ğ“¤ ğ“¥ := ğ“¤.Hom ğ“¥
  id := OpenCover.Hom.id
  comp f g := f.comp g

@[simp]
lemma OpenCover.id_idx_apply {X : Scheme.{u}} (ğ“¤ : X.OpenCover) (j : ğ“¤.J) :
    (ğŸ™ ğ“¤ : ğ“¤ âŸ¶ ğ“¤).idx j = j := rfl

@[simp]
lemma OpenCover.id_app {X : Scheme.{u}} (ğ“¤ : X.OpenCover) (j : ğ“¤.J) :
    (ğŸ™ ğ“¤ : ğ“¤ âŸ¶ ğ“¤).app j = ğŸ™ _ := rfl

@[simp]
lemma OpenCover.comp_idx_apply {X : Scheme.{u}} {ğ“¤ ğ“¥ ğ“¦ : X.OpenCover}
    (f : ğ“¤ âŸ¶ ğ“¥) (g : ğ“¥ âŸ¶ ğ“¦) (j : ğ“¤.J) :
    (f â‰« g).idx j = g.idx (f.idx j) := rfl

@[simp]
lemma OpenCover.comp_app {X : Scheme.{u}} {ğ“¤ ğ“¥ ğ“¦ : X.OpenCover}
    (f : ğ“¤ âŸ¶ ğ“¥) (g : ğ“¥ âŸ¶ ğ“¦) (j : ğ“¤.J) :
    (f â‰« g).app j = f.app j â‰« g.app _ := rfl

end category

/-- Given any open cover `ğ“¤`, this is an affine open cover which refines it. -/
def OpenCover.fromAffineRefinement {X : Scheme.{u}} (ğ“¤ : X.OpenCover) :
    ğ“¤.affineRefinement.openCover âŸ¶ ğ“¤ where
  idx j := j.fst
  app j := (ğ“¤.obj j.fst).affineCover.map _

section deprecated

/-- The basic open sets form an affine open cover of `Spec R`. -/
def affineBasisCoverOfAffine (R : CommRingCat.{u}) : OpenCover (Spec.obj (Opposite.op R)) where
  J := R
  obj r := Spec.obj (Opposite.op <| CommRingCat.of <| Localization.Away r)
  map r := Spec.map (Quiver.Hom.op (algebraMap R (Localization.Away r) : _))
  f _ := 1
  Covers r := by
    rw [Set.range_iff_surjective.mpr ((TopCat.epi_iff_surjective _).mp _)]
    Â· exact trivial
    Â· -- Porting note: need more hand holding here because Lean knows that
      -- `CommRing.ofHom ...` is iso, but without `ofHom` Lean does not know what to do
      change Epi (Spec.map (CommRingCat.ofHom (algebraMap _ _)).op).1.base
      infer_instance
  IsOpen x := AlgebraicGeometry.Scheme.basic_open_isOpenImmersion x
#align algebraic_geometry.Scheme.affine_basis_cover_of_affine AlgebraicGeometry.Scheme.affineBasisCoverOfAffine

/-- We may bind the basic open sets of an open affine cover to form an affine cover that is also
a basis. -/
def affineBasisCover (X : Scheme.{u}) : OpenCover X :=
  X.affineCover.bind fun _ => affineBasisCoverOfAffine _
#align algebraic_geometry.Scheme.affine_basis_cover AlgebraicGeometry.Scheme.affineBasisCover

/-- The coordinate ring of a component in the `affine_basis_cover`. -/
def affineBasisCoverRing (X : Scheme.{u}) (i : X.affineBasisCover.J) : CommRingCat :=
  CommRingCat.of <| @Localization.Away (X.local_affine i.1).choose_spec.choose _ i.2
#align algebraic_geometry.Scheme.affine_basis_cover_ring AlgebraicGeometry.Scheme.affineBasisCoverRing

theorem affineBasisCover_obj (X : Scheme.{u}) (i : X.affineBasisCover.J) :
    X.affineBasisCover.obj i = Spec.obj (op <| X.affineBasisCoverRing i) :=
  rfl
#align algebraic_geometry.Scheme.affine_basis_cover_obj AlgebraicGeometry.Scheme.affineBasisCover_obj

theorem affineBasisCover_map_range (X : Scheme.{u}) (x : X)
    (r : (X.local_affine x).choose_spec.choose) :
    Set.range (X.affineBasisCover.map âŸ¨x, râŸ©).1.base =
      (X.affineCover.map x).1.base '' (PrimeSpectrum.basicOpen r).1 := by
  erw [coe_comp, Set.range_comp]
  -- Porting note: `congr` fails to see the goal is comparing image of the same function
  refine congr_arg (_ '' Â·) ?_
  exact (PrimeSpectrum.localization_away_comap_range (Localization.Away r) r : _)
#align algebraic_geometry.Scheme.affine_basis_cover_map_range AlgebraicGeometry.Scheme.affineBasisCover_map_range

theorem affineBasisCover_is_basis (X : Scheme.{u}) :
    TopologicalSpace.IsTopologicalBasis
      {x : Set X |
        âˆƒ a : X.affineBasisCover.J, x = Set.range (X.affineBasisCover.map a).1.base} := by
  apply TopologicalSpace.isTopologicalBasis_of_isOpen_of_nhds
  Â· rintro _ âŸ¨a, rflâŸ©
    exact IsOpenImmersion.isOpen_range (X.affineBasisCover.map a)
  Â· rintro a U haU hU
    rcases X.affineCover.Covers a with âŸ¨x, eâŸ©
    let U' := (X.affineCover.map (X.affineCover.f a)).1.base â»Â¹' U
    have hxU' : x âˆˆ U' := by rw [â† e] at haU; exact haU
    rcases PrimeSpectrum.isBasis_basic_opens.exists_subset_of_mem_open hxU'
        ((X.affineCover.map (X.affineCover.f a)).1.base.continuous_toFun.isOpen_preimage _
          hU) with
      âŸ¨_, âŸ¨_, âŸ¨s, rflâŸ©, rflâŸ©, hxV, hVUâŸ©
    refine âŸ¨_, âŸ¨âŸ¨_, sâŸ©, rflâŸ©, ?_, ?_âŸ© <;> erw [affineBasisCover_map_range]
    Â· exact âŸ¨x, hxV, eâŸ©
    Â· rw [Set.image_subset_iff]; exact hVU
#align algebraic_geometry.Scheme.affine_basis_cover_is_basis AlgebraicGeometry.Scheme.affineBasisCover_is_basis

end deprecated

end Scheme

end AlgebraicGeometry
