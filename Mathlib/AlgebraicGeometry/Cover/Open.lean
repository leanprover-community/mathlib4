/-
Copyright (c) 2024 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Cover.MorphismProperty

/-!
# Open covers of schemes

This file provides the basic API for open covers of schemes.

## Main definition
- `AlgebraicGeometry.Scheme.OpenCover`: The type of open covers of a scheme `X`,
  consisting of a family of open immersions into `X`,
  and for each `x : X` an open immersion (indexed by `f x`) that covers `x`.
- `AlgebraicGeometry.Scheme.affineCover`: `X.affineCover` is a choice of an affine cover of `X`.
- `AlgebraicGeometry.Scheme.AffineOpenCover`: The type of affine open covers of a scheme `X`.
-/


noncomputable section

open TopologicalSpace CategoryTheory Opposite CategoryTheory.Limits

universe v v‚ÇÅ v‚ÇÇ u

namespace AlgebraicGeometry

namespace Scheme

instance : MorphismProperty.HasPullbacks IsOpenImmersion where
  hasPullback _ _ := inferInstance

/-- An open cover of a scheme `X` is a cover where all component maps are open immersions. -/
abbrev OpenCover (X : Scheme.{u}) : Type _ := Cover.{v} (precoverage @IsOpenImmersion) X

variable {X Y Z : Scheme.{u}} (ùí∞ : OpenCover X) (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z)
variable [‚àÄ x, HasPullback (ùí∞.f x ‚â´ f) g]

instance (i : ùí∞.I‚ÇÄ) : IsOpenImmersion (ùí∞.f i) := ùí∞.map_prop i

/-- The affine cover of a scheme. -/
def affineCover (X : Scheme.{u}) : OpenCover X where
  I‚ÇÄ := X
  X x := Spec (X.local_affine x).choose_spec.choose
  f x :=
    ‚ü®(X.local_affine x).choose_spec.choose_spec.some.inv ‚â´ X.toLocallyRingedSpace.ofRestrict _‚ü©
  mem‚ÇÄ := by
    rw [presieve‚ÇÄ_mem_precoverage_iff]
    refine ‚ü®fun x ‚Ü¶ ?_, inferInstance‚ü©
    use x
    simp only [LocallyRingedSpace.comp_toShHom, SheafedSpace.comp_base, TopCat.hom_comp,
      ContinuousMap.coe_comp]
    rw [Set.range_comp, Set.range_eq_univ.mpr, Set.image_univ]
    ¬∑ erw [Subtype.range_coe_subtype]
      exact (X.local_affine x).choose.2
    rw [‚Üê TopCat.epi_iff_surjective]
    change Epi ((SheafedSpace.forget _).map (LocallyRingedSpace.forgetToSheafedSpace.map _))
    infer_instance

instance : Inhabited X.OpenCover :=
  ‚ü®X.affineCover‚ü©

theorem OpenCover.iSup_opensRange {X : Scheme.{u}} (ùí∞ : X.OpenCover) :
    ‚®Ü i, (ùí∞.f i).opensRange = ‚ä§ :=
  Opens.ext <| by rw [Opens.coe_iSup]; exact ùí∞.iUnion_range

/-- The ranges of the maps in a scheme-theoretic open cover are a topological open cover. -/
lemma OpenCover.isOpenCover_opensRange {X : Scheme.{u}} (ùí∞ : X.OpenCover) :
    IsOpenCover fun i ‚Ü¶ (ùí∞.f i).opensRange :=
  .mk ùí∞.iSup_opensRange

/-- Every open cover of a quasi-compact scheme can be refined into a finite subcover.
-/
@[simps! X f]
def OpenCover.finiteSubcover {X : Scheme.{u}} (ùí∞ : OpenCover X) [H : CompactSpace X] :
    OpenCover X := by
  have :=
    @CompactSpace.elim_nhds_subcover _ _ H (fun x : X => Set.range (ùí∞.f (ùí∞.idx x)).base)
      fun x => (IsOpenImmersion.isOpen_range (ùí∞.f (ùí∞.idx x))).mem_nhds (ùí∞.covers x)
  let t := this.choose
  have h : ‚àÄ x : X, ‚àÉ y : t, x ‚àà Set.range (ùí∞.f (ùí∞.idx y)).base := by
    intro x
    have h' : x ‚àà (‚ä§ : Set X) := trivial
    rw [‚Üê Classical.choose_spec this, Set.mem_iUnion] at h'
    rcases h' with ‚ü®y, _, ‚ü®hy, rfl‚ü©, hy'‚ü©
    exact ‚ü®‚ü®y, hy‚ü©, hy'‚ü©
  exact
    { I‚ÇÄ := t
      X := fun x => ùí∞.X (ùí∞.idx x.1)
      f := fun x => ùí∞.f (ùí∞.idx x.1)
      mem‚ÇÄ := by
        rw [presieve‚ÇÄ_mem_precoverage_iff]
        exact ‚ü®h, inferInstance‚ü© }

instance [H : CompactSpace X] : Fintype ùí∞.finiteSubcover.I‚ÇÄ := by
  delta OpenCover.finiteSubcover; infer_instance

theorem OpenCover.compactSpace {X : Scheme.{u}} (ùí∞ : X.OpenCover) [Finite ùí∞.I‚ÇÄ]
    [H : ‚àÄ i, CompactSpace (ùí∞.X i)] : CompactSpace X := by
  cases nonempty_fintype ùí∞.I‚ÇÄ
  rw [‚Üê isCompact_univ_iff, ‚Üê ùí∞.iUnion_range]
  apply isCompact_iUnion
  intro i
  rw [isCompact_iff_compactSpace]
  exact
    @Homeomorph.compactSpace _ _ _ _ (H i)
      (TopCat.homeoOfIso
        (asIso
          (IsOpenImmersion.isoOfRangeEq (ùí∞.f i)
            (X.ofRestrict (Opens.isOpenEmbedding ‚ü®_, (ùí∞.map_prop i).base_open.isOpen_range‚ü©))
            Subtype.range_coe.symm).hom.base))
/--
An affine open cover of `X` consists of a family of open immersions into `X` from
spectra of rings.
-/
abbrev AffineOpenCover (X : Scheme.{u}) : Type _ :=
  AffineCover.{v} @IsOpenImmersion X

namespace AffineOpenCover

instance {X : Scheme.{u}} (ùí∞ : X.AffineOpenCover) (j : ùí∞.I‚ÇÄ) : IsOpenImmersion (ùí∞.f j) :=
  ùí∞.map_prop j

/-- The open cover associated to an affine open cover. -/
@[simps! I‚ÇÄ X f]
def openCover {X : Scheme.{u}} (ùí∞ : X.AffineOpenCover) : X.OpenCover :=
  AffineCover.cover ùí∞

end AffineOpenCover

/-- A choice of an affine open cover of a scheme. -/
@[simps]
def affineOpenCover (X : Scheme.{u}) : X.AffineOpenCover where
  X := _
  I‚ÇÄ := X.affineCover.I‚ÇÄ
  f := X.affineCover.f
  idx x := (X.affineCover.exists_eq x).choose
  covers x := (X.affineCover.exists_eq x).choose_spec

@[simp]
lemma openCover_affineOpenCover (X : Scheme.{u}) : X.affineOpenCover.openCover = X.affineCover :=
  rfl

/-- Given any open cover `ùì§`, this is an affine open cover which refines it.
The morphism in the category of open covers which proves that this is indeed a refinement, see
`AlgebraicGeometry.Scheme.OpenCover.fromAffineRefinement`.
-/
def OpenCover.affineRefinement {X : Scheme.{u}} (ùì§ : X.OpenCover) : X.AffineOpenCover where
  X := _
  I‚ÇÄ := (ùì§.bind fun j => (ùì§.X j).affineCover).I‚ÇÄ
  f := (ùì§.bind fun j => (ùì§.X j).affineCover).f
  idx := Cover.idx (ùì§.bind fun j => (ùì§.X j).affineCover)
  covers := Cover.covers (ùì§.bind fun j => (ùì§.X j).affineCover)

/-- The pullback of the affine refinement is the pullback of the affine cover. -/
def OpenCover.pullbackCoverAffineRefinementObjIso (f : X ‚ü∂ Y) (ùí∞ : Y.OpenCover) (i) :
    (ùí∞.affineRefinement.openCover.pullback‚ÇÅ f).X i ‚âÖ
      ((ùí∞.X i.1).affineCover.pullback‚ÇÅ (ùí∞.pullbackHom f i.1)).X i.2 :=
  pullbackSymmetry _ _ ‚â™‚â´ (pullbackRightPullbackFstIso _ _ _).symm ‚â™‚â´
    pullbackSymmetry _ _ ‚â™‚â´ asIso (pullback.map _ _ _ _ (pullbackSymmetry _ _).hom (ùüô _) (ùüô _)
      (by simp [Cover.pullbackHom]) (by simp))

@[reassoc]
lemma OpenCover.pullbackCoverAffineRefinementObjIso_inv_map (f : X ‚ü∂ Y) (ùí∞ : Y.OpenCover) (i) :
    (ùí∞.pullbackCoverAffineRefinementObjIso f i).inv ‚â´
      (ùí∞.affineRefinement.openCover.pullback‚ÇÅ f).f i =
      ((ùí∞.X i.1).affineCover.pullback‚ÇÅ (ùí∞.pullbackHom f i.1)).f i.2 ‚â´
        (ùí∞.pullback‚ÇÅ f).f i.1 := by
  simp only [Precoverage.ZeroHypercover.pullback‚ÇÅ_toPreZeroHypercover,
    PreZeroHypercover.pullback‚ÇÅ_X, AffineOpenCover.openCover_X, AffineOpenCover.openCover_f,
    pullbackCoverAffineRefinementObjIso, Iso.trans_inv, asIso_inv, Iso.symm_inv, Category.assoc,
    PreZeroHypercover.pullback‚ÇÅ_f, pullbackSymmetry_inv_comp_fst, IsIso.inv_comp_eq,
    limit.lift_œÄ_assoc, PullbackCone.mk_pt, cospan_left, PullbackCone.mk_œÄ_app,
    pullbackSymmetry_hom_comp_fst]
  convert pullbackSymmetry_inv_comp_snd_assoc
    ((ùí∞.X i.1).affineCover.f i.2) (pullback.fst _ _) _ using 2
  exact pullbackRightPullbackFstIso_hom_snd _ _ _

@[reassoc]
lemma OpenCover.pullbackCoverAffineRefinementObjIso_inv_pullbackHom
    (f : X ‚ü∂ Y) (ùí∞ : Y.OpenCover) (i) :
    (ùí∞.pullbackCoverAffineRefinementObjIso f i).inv ‚â´
      ùí∞.affineRefinement.openCover.pullbackHom f i =
      (ùí∞.X i.1).affineCover.pullbackHom (ùí∞.pullbackHom f i.1) i.2 := by
  simp only [Precoverage.ZeroHypercover.pullback‚ÇÅ_toPreZeroHypercover,
    PreZeroHypercover.pullback‚ÇÅ_X, Cover.pullbackHom, AffineOpenCover.openCover_X,
    AffineOpenCover.openCover_f, pullbackCoverAffineRefinementObjIso, Iso.trans_inv, asIso_inv,
    Iso.symm_inv, Category.assoc, pullbackSymmetry_inv_comp_snd, IsIso.inv_comp_eq, limit.lift_œÄ,
    PullbackCone.mk_pt, PullbackCone.mk_œÄ_app, Category.comp_id]
  convert pullbackSymmetry_inv_comp_fst ((ùí∞.X i.1).affineCover.f i.2) (pullback.fst _ _)
  exact pullbackRightPullbackFstIso_hom_fst _ _ _

/-- A family of elements spanning the unit ideal of `R` gives a affine open cover of `Spec R`. -/
@[simps]
noncomputable
def affineOpenCoverOfSpanRangeEqTop {R : CommRingCat} {Œπ : Type*} (s : Œπ ‚Üí R)
    (hs : Ideal.span (Set.range s) = ‚ä§) : (Spec R).AffineOpenCover where
  I‚ÇÄ := Œπ
  X i := .of (Localization.Away (s i))
  f i := Spec.map (CommRingCat.ofHom (algebraMap R (Localization.Away (s i))))
  idx x := by
    have : ‚àÉ i, s i ‚àâ x.asIdeal := by
      by_contra! h; apply x.2.ne_top; rwa [‚Üê top_le_iff, ‚Üê hs, Ideal.span_le, Set.range_subset_iff]
    exact this.choose
  covers x := by
    generalize_proofs H
    let i := H.choose
    have := PrimeSpectrum.localization_away_comap_range (Localization.Away (s i)) (s i)
    exact (eq_iff_iff.mp congr(x ‚àà $this)).mpr H.choose_spec

/-- Given any open cover `ùì§`, this is an affine open cover which refines it. -/
def OpenCover.fromAffineRefinement {X : Scheme.{u}} (ùì§ : X.OpenCover) :
    ùì§.affineRefinement.openCover ‚ü∂ ùì§ where
  idx j := j.fst
  app j := (ùì§.X j.fst).affineCover.f _

/-- If two global sections agree after restriction to each member of an open cover, then
they agree globally. -/
lemma OpenCover.ext_elem {X : Scheme.{u}} {U : X.Opens} (f g : Œì(X, U)) (ùí∞ : X.OpenCover)
    (h : ‚àÄ i : ùí∞.I‚ÇÄ, (ùí∞.f i).app U f = (ùí∞.f i).app U g) : f = g := by
  fapply TopCat.Sheaf.eq_of_locally_eq' X.sheaf
    (fun i ‚Ü¶ (ùí∞.f (ùí∞.idx i)).opensRange ‚äì U) _ (fun _ ‚Ü¶ homOfLE inf_le_right)
  ¬∑ intro x hx
    simp only [Opens.iSup_mk, Opens.carrier_eq_coe, Opens.coe_inf, Hom.coe_opensRange, Opens.coe_mk,
      Set.mem_iUnion, Set.mem_inter_iff, Set.mem_range, SetLike.mem_coe, exists_and_right]
    refine ‚ü®?_, hx‚ü©
    simpa using ‚ü®_, ùí∞.covers x‚ü©
  ¬∑ intro x
    replace h := h (ùí∞.idx x)
    rw [‚Üê IsOpenImmersion.map_ŒìIso_inv] at h
    exact (IsOpenImmersion.ŒìIso (ùí∞.f (ùí∞.idx x)) U).commRingCatIsoToRingEquiv.symm.injective h

/-- If the restriction of a global section to each member of an open cover is zero, then it is
globally zero. -/
lemma zero_of_zero_cover {X : Scheme.{u}} {U : X.Opens} (s : Œì(X, U)) (ùí∞ : X.OpenCover)
    (h : ‚àÄ i : ùí∞.I‚ÇÄ, (ùí∞.f i).app U s = 0) : s = 0 :=
  ùí∞.ext_elem s 0 (fun i ‚Ü¶ by rw [map_zero]; exact h i)

/-- If a global section is nilpotent on each member of a finite open cover, then `f` is
nilpotent. -/
lemma isNilpotent_of_isNilpotent_cover {X : Scheme.{u}} {U : X.Opens} (s : Œì(X, U))
    (ùí∞ : X.OpenCover) [Finite ùí∞.I‚ÇÄ] (h : ‚àÄ i : ùí∞.I‚ÇÄ, IsNilpotent ((ùí∞.f i).app U s)) :
    IsNilpotent s := by
  choose fn hfn using h
  have : Fintype ùí∞.I‚ÇÄ := Fintype.ofFinite ùí∞.I‚ÇÄ
  /- the maximum of all `fn i` (exists, because `ùí∞.I‚ÇÄ` is finite) -/
  let N : ‚Ñï := Finset.sup Finset.univ fn
  have hfnleN (i : ùí∞.I‚ÇÄ) : fn i ‚â§ N := Finset.le_sup (Finset.mem_univ i)
  use N
  apply zero_of_zero_cover (ùí∞ := ùí∞)
  on_goal 1 => intro i; simp only [map_pow]
  -- This closes both remaining goals at once.
  exact pow_eq_zero_of_le (hfnleN i) (hfn i)

section deprecated

/-- The basic open sets form an affine open cover of `Spec R`. -/
def affineBasisCoverOfAffine (R : CommRingCat.{u}) : OpenCover (Spec R) where
  I‚ÇÄ := R
  X r := Spec <| .of <| Localization.Away r
  f r := Spec.map (CommRingCat.ofHom (algebraMap R (Localization.Away r)))
  mem‚ÇÄ := by
    rw [presieve‚ÇÄ_mem_precoverage_iff]
    refine ‚ü®fun x ‚Ü¶ ‚ü®1, ?_‚ü©, AlgebraicGeometry.Scheme.basic_open_isOpenImmersion‚ü©
    rw [Set.range_eq_univ.mpr ((TopCat.epi_iff_surjective _).mp _)]
    ¬∑ exact trivial
    ¬∑ infer_instance

/-- We may bind the basic open sets of an open affine cover to form an affine cover that is also
a basis. -/
def affineBasisCover (X : Scheme.{u}) : OpenCover X :=
  X.affineCover.bind fun _ => affineBasisCoverOfAffine _

/-- The coordinate ring of a component in the `affine_basis_cover`. -/
def affineBasisCoverRing (X : Scheme.{u}) (i : X.affineBasisCover.I‚ÇÄ) : CommRingCat :=
  CommRingCat.of <| @Localization.Away (X.local_affine i.1).choose_spec.choose _ i.2

theorem affineBasisCover_obj (X : Scheme.{u}) (i : X.affineBasisCover.I‚ÇÄ) :
    X.affineBasisCover.X i = Spec (X.affineBasisCoverRing i) :=
  rfl

theorem affineBasisCover_map_range (X : Scheme.{u}) (x : X)
    (r : (X.local_affine x).choose_spec.choose) :
    Set.range (X.affineBasisCover.f ‚ü®x, r‚ü©).base =
      (X.affineCover.f x).base '' (PrimeSpectrum.basicOpen r).1 := by
  simp only [affineBasisCover, Precoverage.ZeroHypercover.bind_toPreZeroHypercover,
    PreZeroHypercover.bind_f, comp_coeBase, TopCat.hom_comp, ContinuousMap.coe_comp, Set.range_comp]
  congr
  exact (PrimeSpectrum.localization_away_comap_range (Localization.Away r) r :)

theorem affineBasisCover_is_basis (X : Scheme.{u}) :
    TopologicalSpace.IsTopologicalBasis
      {x : Set X |
        ‚àÉ a : X.affineBasisCover.I‚ÇÄ, x = Set.range (X.affineBasisCover.f a).base} := by
  apply TopologicalSpace.isTopologicalBasis_of_isOpen_of_nhds
  ¬∑ rintro _ ‚ü®a, rfl‚ü©
    exact IsOpenImmersion.isOpen_range (X.affineBasisCover.f a)
  ¬∑ rintro a U haU hU
    rcases X.affineCover.covers a with ‚ü®x, e‚ü©
    let U' := (X.affineCover.f (X.affineCover.idx a)).base ‚Åª¬π' U
    have hxU' : x ‚àà U' := by rw [‚Üê e] at haU; exact haU
    rcases PrimeSpectrum.isBasis_basic_opens.exists_subset_of_mem_open hxU'
        ((X.affineCover.f (X.affineCover.idx a)).base.hom.continuous_toFun.isOpen_preimage _
          hU) with
      ‚ü®_, ‚ü®_, ‚ü®s, rfl‚ü©, rfl‚ü©, hxV, hVU‚ü©
    refine ‚ü®_, ‚ü®‚ü®_, s‚ü©, rfl‚ü©, ?_, ?_‚ü© <;> rw [affineBasisCover_map_range]
    ¬∑ exact ‚ü®x, hxV, e‚ü©
    ¬∑ rw [Set.image_subset_iff]; exact hVU

end deprecated

end Scheme

end AlgebraicGeometry
