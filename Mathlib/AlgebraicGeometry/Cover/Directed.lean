/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.AlgebraicGeometry.PullbackCarrier

/-!
# Locally directed covers

A locally directed `P`-cover of a scheme `X` is a cover `ğ’°` with an ordering
on the indices and compatible transition maps `ğ’°áµ¢ âŸ¶ ğ’°â±¼` for `i â‰¤ j` such that
every `x : ğ’°áµ¢ Ã—[X] ğ’°â±¼` comes from some `ğ’°â‚–` for a `k â‰¤ i` and `k â‰¤ j`.

Gluing along directed covers is easier, because the intersections `ğ’°áµ¢ Ã—[X] ğ’°â±¼` can
be covered by a subcover of `ğ’°`.

Many natural covers are naturally directed, most importantly the cover of all affine
opens of a scheme.
-/

universe u

noncomputable section

open CategoryTheory Limits

namespace AlgebraicGeometry.Scheme

variable {P : MorphismProperty Scheme.{u}} {X : Scheme.{u}}

/-- A directed `P`-cover of a scheme `X` is a cover `ğ’°` with an ordering
on the indices and compatible transition maps `ğ’°áµ¢ âŸ¶ ğ’°â±¼` for `i â‰¤ j` such that
every `x : ğ’°áµ¢ Ã—[X] ğ’°â±¼` comes from some `ğ’°â‚–` for a `k â‰¤ i` and `k â‰¤ j`. -/
class Cover.LocallyDirected (ğ’° : X.Cover P) extends LE ğ’°.J where
  /-- The transition map `ğ’°áµ¢ âŸ¶ ğ’°â±¼` for `i â‰¤ j`. -/
  trans {i j : ğ’°.J} (hij : i â‰¤ j) : ğ’°.obj i âŸ¶ ğ’°.obj j
  w {i j : ğ’°.J} (hij : i â‰¤ j) : trans hij â‰« ğ’°.map j = ğ’°.map i := by aesop_cat
  directed {i j : ğ’°.J} (x : (pullback (ğ’°.map i) (ğ’°.map j)).carrier) :
    âˆƒ (k : ğ’°.J) (hki : k â‰¤ i) (hkj : k â‰¤ j) (y : ğ’°.obj k),
      (pullback.lift (trans hki) (trans hkj) (by simp [w])).base y = x
  property_trans {i j : ğ’°.J} (hij : i â‰¤ j) : P (trans hij) := by infer_instance

variable (ğ’° : X.Cover P) [ğ’°.LocallyDirected]

/-- The transition maps of a directed cover. -/
def Cover.trans {i j : ğ’°.J} (hij : i â‰¤ j) : ğ’°.obj i âŸ¶ ğ’°.obj j := LocallyDirected.trans hij

@[simp]
lemma Cover.trans_map {i j : ğ’°.J} (hij : i â‰¤ j) : ğ’°.trans hij â‰« ğ’°.map j = ğ’°.map i :=
  LocallyDirected.w hij

lemma Cover.exists_lift_trans_eq {i j : ğ’°.J} (x : (pullback (ğ’°.map i) (ğ’°.map j)).carrier) :
    âˆƒ (k : ğ’°.J) (hki : k â‰¤ i) (hkj : k â‰¤ j) (y : ğ’°.obj k),
      (pullback.lift (ğ’°.trans hki) (ğ’°.trans hkj) (by simp)).base y = x :=
  LocallyDirected.directed x

lemma Cover.property_trans {i j : ğ’°.J} (hij : i â‰¤ j) : P (ğ’°.trans hij) :=
  LocallyDirected.property_trans hij

/-- If `ğ’°` is a directed cover of `X`, this is the cover of `ğ’°áµ¢ Ã—[X] ğ’°â±¼` by `{ğ’°â‚–}` where
`k â‰¤ i` and `k â‰¤ j`. -/
@[simps map]
def Cover.intersectionOfLocallyDirected [P.IsStableUnderBaseChange] [P.HasOfPostcompProperty P]
    (i j : ğ’°.J) : (pullback (ğ’°.map i) (ğ’°.map j)).Cover P where
  J := { k : ğ’°.J | k â‰¤ i âˆ§ k â‰¤ j }
  obj k := ğ’°.obj k
  map k := pullback.lift (ğ’°.trans k.2.1) (ğ’°.trans k.2.2) (by simp)
  f x := âŸ¨(ğ’°.exists_lift_trans_eq x).choose, (ğ’°.exists_lift_trans_eq x).choose_spec.choose,
    (ğ’°.exists_lift_trans_eq x).choose_spec.choose_spec.chooseâŸ©
  covers x := (ğ’°.exists_lift_trans_eq x).choose_spec.choose_spec.choose_spec
  map_prop k := by
    apply P.of_postcomp (W' := P) _ (pullback.fst _ _) (P.pullback_fst _ _ (ğ’°.map_prop _))
    rw [pullback.lift_fst]
    exact ğ’°.property_trans _

/-- If `ğ’°` is a directed open cover of `X`, to glue morphisms `{gáµ¢ : ğ’°áµ¢ âŸ¶ Y}` it suffices
to check compatibility with the transition maps. -/
def OpenCover.glueMorphismsOfLocallyDirected (ğ’° : X.OpenCover) [ğ’°.LocallyDirected] {Y : Scheme.{u}}
    (g : âˆ€ i, ğ’°.obj i âŸ¶ Y) (h : âˆ€ {i j : ğ’°.J} (hij : i â‰¤ j), ğ’°.trans hij â‰« g j = g i) :
    X âŸ¶ Y :=
  ğ’°.glueMorphisms g <| fun i j â†¦ by
    apply (ğ’°.intersectionOfLocallyDirected i j).hom_ext
    intro k
    simp [h]

@[reassoc (attr := simp)]
lemma OpenCover.map_glueMorphismsOfLocallyDirected (ğ’° : X.OpenCover) [ğ’°.LocallyDirected]
    {Y : Scheme.{u}} (g : âˆ€ i, ğ’°.obj i âŸ¶ Y)
    (h : âˆ€ {i j : ğ’°.J} (hij : i â‰¤ j), ğ’°.trans hij â‰« g j = g i) (i : ğ’°.J) :
    ğ’°.map i â‰« ğ’°.glueMorphismsOfLocallyDirected g h = g i := by
  simp [glueMorphismsOfLocallyDirected]

/-- If `ğ’°` is a directed open cover of `X`, to glue morphisms `{gáµ¢ : ğ’°áµ¢ âŸ¶ Y}` over `S` it suffices
to check compatibility with the transition maps. -/
def OpenCover.glueMorphismsOverOfLocallyDirected {S : Scheme.{u}} {X : Over S}
    (ğ’° : X.left.OpenCover) [ğ’°.LocallyDirected] {Y : Over S} (g : âˆ€ i, ğ’°.obj i âŸ¶ Y.left)
    (h : âˆ€ {i j : ğ’°.J} (hij : i â‰¤ j), ğ’°.trans hij â‰« g j = g i)
    (w : âˆ€ i, g i â‰« Y.hom = ğ’°.map i â‰« X.hom) :
    X âŸ¶ Y :=
  Over.homMk (ğ’°.glueMorphismsOfLocallyDirected g h) <| by
    apply ğ’°.hom_ext
    intro i
    simp [w]

@[reassoc (attr := simp)]
lemma OpenCover.map_glueMorphismsOverOfLocallyDirected_left {S : Scheme.{u}} {X : Over S}
    (ğ’° : X.left.OpenCover) [ğ’°.LocallyDirected] {Y : Over S} (g : âˆ€ i, ğ’°.obj i âŸ¶ Y.left)
    (h : âˆ€ {i j : ğ’°.J} (hij : i â‰¤ j), ğ’°.trans hij â‰« g j = g i)
    (w : âˆ€ i, g i â‰« Y.hom = ğ’°.map i â‰« X.hom) (i : ğ’°.J) :
    ğ’°.map i â‰« (ğ’°.glueMorphismsOverOfLocallyDirected g h w).left = g i := by
  simp [glueMorphismsOverOfLocallyDirected]

instance Cover.locallyDirectedPullbackCover [P.IsStableUnderBaseChange] (ğ’° : X.Cover P)
    [ğ’°.LocallyDirected] {Y : Scheme.{u}} (f : Y âŸ¶ X) :
    (ğ’°.pullbackCover f).LocallyDirected where
  le (x : ğ’°.J) (y : ğ’°.J) := x â‰¤ y
  trans {i j} hij := pullback.map f (ğ’°.map i) f (ğ’°.map j) (ğŸ™ _) (ğ’°.trans hij) (ğŸ™ _)
    (by simp) (by simp)
  directed {i j} x := by
    dsimp at i j x âŠ¢
    let iso : pullback (pullback.fst f (ğ’°.map i)) (pullback.fst f (ğ’°.map j)) â‰…
        pullback f (pullback.fst (ğ’°.map i) (ğ’°.map j) â‰« ğ’°.map i) :=
      pullbackRightPullbackFstIso _ _ _ â‰ªâ‰« pullback.congrHom pullback.condition rfl â‰ªâ‰«
        pullbackAssoc ..
    have (k : ğ’°.J) (hki : k â‰¤ i) (hkj : k â‰¤ j) :
        (pullback.lift
          (pullback.map f (ğ’°.map k) f (ğ’°.map i) (ğŸ™ Y) (ğ’°.trans hki) (ğŸ™ X) (by simp) (by simp))
          (pullback.map f (ğ’°.map k) f (ğ’°.map j) (ğŸ™ Y) (ğ’°.trans hkj) (ğŸ™ X) (by simp) (by simp))
            (by simp)) =
          pullback.map _ _ _ _ (ğŸ™ Y) (pullback.lift (ğ’°.trans hki) (ğ’°.trans hkj) (by simp)) (ğŸ™ X)
            (by simp) (by simp) â‰« iso.inv := by
      apply pullback.hom_ext <;> apply pullback.hom_ext <;> simp [iso]
    obtain âŸ¨k, hki, hkj, yk, hykâŸ© := ğ’°.exists_lift_trans_eq ((iso.hom â‰« pullback.snd _ _).base x)
    refine âŸ¨k, hki, hkj, show x âˆˆ Set.range _ from ?_âŸ©
    rw [this, Scheme.comp_base, TopCat.coe_comp, Set.range_comp, Pullback.range_map]
    use iso.hom.base x
    simp only [id.base, TopCat.hom_id, ContinuousMap.coe_id, Set.range_id, Set.preimage_univ,
      Set.univ_inter, Set.mem_preimage, Set.mem_range, iso_hom_base_inv_base_apply, and_true]
    exact âŸ¨yk, hykâŸ©
  property_trans {i j} hij := by
    let iso : pullback f (ğ’°.map i) â‰… pullback (pullback.snd f (ğ’°.map j)) (ğ’°.trans hij) :=
      pullback.congrHom rfl (by simp) â‰ªâ‰« (pullbackLeftPullbackSndIso _ _ _).symm
    rw [â† P.cancel_left_of_respectsIso iso.inv]
    simp only [pullbackCover_obj, Iso.trans_inv, Iso.symm_inv, pullback.congrHom_inv,
      Category.assoc, iso]
    convert P.pullback_fst _ _ (ğ’°.property_trans hij)
    apply pullback.hom_ext <;> simp [pullback.condition]

/-- If `ğ’°` is an open cover such that the images of the components form a basis of the topology
of `X`, `ğ’°` is directed by the ordering of subset inclusion of the images. -/
def Cover.LocallyDirected.ofIsBasisOpensRange {ğ’° : X.OpenCover} [LE ğ’°.J]
    (hle : âˆ€ {i j : ğ’°.J}, i â‰¤ j â†” (ğ’°.map i).opensRange â‰¤ (ğ’°.map j).opensRange)
    (H : TopologicalSpace.Opens.IsBasis (Set.range <| fun i â†¦ (ğ’°.map i).opensRange)) :
    ğ’°.LocallyDirected where
  trans {i j} hij := IsOpenImmersion.lift (ğ’°.map j) (ğ’°.map i) (hle.mp hij)
  directed {i j} x := by
    have : (pullback.fst (ğ’°.map i) (ğ’°.map j) â‰« ğ’°.map i).base x âˆˆ
      (pullback.fst (ğ’°.map i) (ğ’°.map j) â‰« ğ’°.map i).opensRange := âŸ¨x, rflâŸ©
    obtain âŸ¨k, âŸ¨k, rflâŸ©, âŸ¨y, hyâŸ©, hâŸ© := TopologicalSpace.Opens.isBasis_iff_nbhd.mp H this
    refine âŸ¨k, hle.mpr <| le_trans h ?_, hle.mpr <| le_trans h ?_, y, ?_âŸ©
    Â· rw [Scheme.Hom.opensRange_comp]
      exact Set.image_subset_range _ _
    Â· simp_rw [pullback.condition, Scheme.Hom.opensRange_comp]
      exact Set.image_subset_range _ _
    Â· apply (pullback.fst (ğ’°.map i) (ğ’°.map j) â‰« ğ’°.map i).isOpenEmbedding.injective
      rw [â† Scheme.comp_base_apply, pullback.lift_fst_assoc, IsOpenImmersion.lift_fac, hy]

section

variable {ğ’° : X.OpenCover} [LE ğ’°.J]
  (hle : âˆ€ {i j : ğ’°.J}, i â‰¤ j â†” (ğ’°.map i).opensRange â‰¤ (ğ’°.map j).opensRange)
  (H : TopologicalSpace.Opens.IsBasis (Set.range <| fun i â†¦ (ğ’°.map i).opensRange))

include hle in
lemma Cover.LocallyDirected.ofIsBasisOpensRange_le_iff (i j : ğ’°.J) :
    letI := Cover.LocallyDirected.ofIsBasisOpensRange hle H
    i â‰¤ j â†” (ğ’°.map i).opensRange â‰¤ (ğ’°.map j).opensRange := hle

lemma Cover.LocallyDirected.ofIsBasisOpensRange_trans {i j : ğ’°.J} :
    letI := Cover.LocallyDirected.ofIsBasisOpensRange hle H
    (hij : i â‰¤ j) â†’ ğ’°.trans hij = IsOpenImmersion.lift (ğ’°.map j) (ğ’°.map i) (hle.mp hij) :=
  fun _ â†¦ rfl

end

variable (X) in
open TopologicalSpace.Opens in
/-- The directed affine open cover of `X` given by all affine opens. -/
@[simps J obj map]
def directedAffineCover : X.OpenCover where
  J := X.affineOpens
  obj U := U
  map U := U.1.Î¹
  f x := âŸ¨(isBasis_iff_nbhd.mp (isBasis_affine_open X) (mem_top x)).choose,
    (isBasis_iff_nbhd.mp (isBasis_affine_open X) (mem_top x)).choose_spec.1âŸ©
  covers x := by
    simpa using (isBasis_iff_nbhd.mp (isBasis_affine_open X) (mem_top x)).choose_spec.2.1

instance : LE X.directedAffineCover.J := inferInstanceAs <| LE X.affineOpens

instance : Scheme.Cover.LocallyDirected X.directedAffineCover :=
  .ofIsBasisOpensRange (by simp) <| by
    convert isBasis_affine_open X
    simp

@[simp]
lemma directedAffineCover_trans {U V : X.affineOpens} (hUV : U â‰¤ V) :
    Cover.trans X.directedAffineCover hUV = X.homOfLE hUV := rfl

end AlgebraicGeometry.Scheme
