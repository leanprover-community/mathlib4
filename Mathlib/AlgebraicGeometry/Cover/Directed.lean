/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.CategoryTheory.LocallyDirected
import Mathlib.AlgebraicGeometry.PullbackCarrier
import Mathlib.AlgebraicGeometry.Gluing

/-!
# Locally directed covers

A locally directed `P`-cover of a scheme `X` is a cover `ğ’°` with an ordering
on the indices and compatible transition maps `ğ’°áµ¢ âŸ¶ ğ’°â±¼` for `i â‰¤ j` such that
every `x : ğ’°áµ¢ Ã—[X] ğ’°â±¼` comes from some `ğ’°â‚–` for a `k â‰¤ i` and `k â‰¤ j`.

Gluing along directed covers is easier, because the intersections `ğ’°áµ¢ Ã—[X] ğ’°â±¼` can
be covered by a subcover of `ğ’°`. In particular, if `ğ’°` is a Zariski cover,
`X` naturally is the colimit of the `ğ’°áµ¢`.

Many natural covers are naturally directed, most importantly the cover of all affine
opens of a scheme.
-/

universe u

noncomputable section

open CategoryTheory Limits

namespace AlgebraicGeometry.Scheme

variable {P : MorphismProperty Scheme.{u}} {X : Scheme.{u}}

namespace Cover

/-- A directed `P`-cover of a scheme `X` is a cover `ğ’°` with an ordering
on the indices and compatible transition maps `ğ’°áµ¢ âŸ¶ ğ’°â±¼` for `i â‰¤ j` such that
every `x : ğ’°áµ¢ Ã—[X] ğ’°â±¼` comes from some `ğ’°â‚–` for a `k â‰¤ i` and `k â‰¤ j`. -/
class LocallyDirected (ğ’° : X.Cover (precoverage P)) [Category ğ’°.Iâ‚€] where
  /-- The transition map `ğ’°áµ¢ âŸ¶ ğ’°â±¼` for `i â‰¤ j`. -/
  trans {i j : ğ’°.Iâ‚€} (hij : i âŸ¶ j) : ğ’°.X i âŸ¶ ğ’°.X j
  trans_id (i : ğ’°.Iâ‚€) : trans (ğŸ™ i) = ğŸ™ (ğ’°.X i)
  trans_comp {i j k : ğ’°.Iâ‚€} (hij : i âŸ¶ j) (hjk : j âŸ¶ k): trans (hij â‰« hjk) = trans hij â‰« trans hjk
  w {i j : ğ’°.Iâ‚€} (hij : i âŸ¶ j) : trans hij â‰« ğ’°.f j = ğ’°.f i := by aesop_cat
  directed {i j : ğ’°.Iâ‚€} (x : (pullback (ğ’°.f i) (ğ’°.f j)).carrier) :
    âˆƒ (k : ğ’°.Iâ‚€) (hki : k âŸ¶ i) (hkj : k âŸ¶ j) (y : ğ’°.X k),
      (pullback.lift (trans hki) (trans hkj) (by simp [w])).base y = x
  property_trans {i j : ğ’°.Iâ‚€} (hij : i âŸ¶ j) : P (trans hij) := by infer_instance

variable (ğ’° : X.Cover (precoverage P)) [Category ğ’°.Iâ‚€] [ğ’°.LocallyDirected]

/-- The transition maps of a directed cover. -/
def trans {i j : ğ’°.Iâ‚€} (hij : i âŸ¶ j) : ğ’°.X i âŸ¶ ğ’°.X j := LocallyDirected.trans hij

@[simp]
lemma trans_map {i j : ğ’°.Iâ‚€} (hij : i âŸ¶ j) : ğ’°.trans hij â‰« ğ’°.f j = ğ’°.f i :=
  LocallyDirected.w hij

@[simp]
lemma trans_id (i : ğ’°.Iâ‚€) : ğ’°.trans (ğŸ™ i) = ğŸ™ (ğ’°.X i) := LocallyDirected.trans_id i

@[simp]
lemma trans_comp {i j k : ğ’°.Iâ‚€} (hij : i âŸ¶ j) (hjk : j âŸ¶ k) :
    ğ’°.trans (hij â‰« hjk) = ğ’°.trans hij â‰« ğ’°.trans hjk := LocallyDirected.trans_comp hij hjk

lemma exists_lift_trans_eq {i j : ğ’°.Iâ‚€} (x : (pullback (ğ’°.f i) (ğ’°.f j)).carrier) :
    âˆƒ (k : ğ’°.Iâ‚€) (hki : k âŸ¶ i) (hkj : k âŸ¶ j) (y : ğ’°.X k),
      (pullback.lift (ğ’°.trans hki) (ğ’°.trans hkj) (by simp)).base y = x :=
  LocallyDirected.directed x

lemma property_trans {i j : ğ’°.Iâ‚€} (hij : i âŸ¶ j) : P (ğ’°.trans hij) :=
  LocallyDirected.property_trans hij

/-- If `ğ’°` is a directed cover of `X`, this is the cover of `ğ’°áµ¢ Ã—[X] ğ’°â±¼` by `{ğ’°â‚–}` where
`k â‰¤ i` and `k â‰¤ j`. -/
@[simps f]
def intersectionOfLocallyDirected [P.IsStableUnderBaseChange] [P.HasOfPostcompProperty P]
    (i j : ğ’°.Iâ‚€) : (pullback (ğ’°.f i) (ğ’°.f j)).Cover (precoverage P) where
  Iâ‚€ := Î£ (k : ğ’°.Iâ‚€), (k âŸ¶ i) Ã— (k âŸ¶ j)
  X k := ğ’°.X k.1
  f k := pullback.lift (ğ’°.trans k.2.1) (ğ’°.trans k.2.2) (by simp)
  memâ‚€ := by
    rw [presieveâ‚€_mem_precoverage_iff]
    refine âŸ¨fun x â†¦ ?_, fun k â†¦ ?_âŸ©
    Â· use âŸ¨(ğ’°.exists_lift_trans_eq x).choose, (ğ’°.exists_lift_trans_eq x).choose_spec.choose,
        (ğ’°.exists_lift_trans_eq x).choose_spec.choose_spec.chooseâŸ©
      exact (ğ’°.exists_lift_trans_eq x).choose_spec.choose_spec.choose_spec
    Â· apply P.of_postcomp (W' := P) _ (pullback.fst _ _) (P.pullback_fst _ _ (ğ’°.map_prop _))
      rw [pullback.lift_fst]
      exact ğ’°.property_trans _

/-- The canonical diagram induced by a locally directed cover. -/
@[simps]
def functorOfLocallyDirected : ğ’°.Iâ‚€ â¥¤ Scheme.{u} where
  obj := ğ’°.X
  map := ğ’°.trans

instance : (ğ’°.functorOfLocallyDirected â‹™ Scheme.forget).IsLocallyDirected where
  cond {i j k} fi fj xi xj hxij := by
    simp only [Functor.comp_obj, Cover.functorOfLocallyDirected_obj, forget_obj, Functor.comp_map,
      Cover.functorOfLocallyDirected_map, forget_map] at hxij
    have : (ğ’°.f i).base xi = (ğ’°.f j).base xj := by
      rw [â† ğ’°.trans_map fi, â† ğ’°.trans_map fj, Hom.comp_base, Hom.comp_base,
        ConcreteCategory.comp_apply, hxij, ConcreteCategory.comp_apply]
    obtain âŸ¨z, rfl, rflâŸ© := Scheme.Pullback.exists_preimage_pullback xi xj this
    obtain âŸ¨l, gi, gj, y, rflâŸ© := ğ’°.exists_lift_trans_eq z
    refine âŸ¨l, gi, gj, y, ?_, ?_âŸ© <;> simp [â† Scheme.Hom.comp_apply]

/--
The canonical cocone with point `X` on the functor induced by the locally directed cover `ğ’°`.
If `ğ’°` is an open cover, this is colimiting (see `OpenCover.isColimitCoconeOfLocallyDirected`).
-/
@[simps]
def coconeOfLocallyDirected : Cocone ğ’°.functorOfLocallyDirected where
  pt := X
  Î¹.app := ğ’°.f

section BaseChange

variable [P.IsStableUnderBaseChange] (ğ’° : X.Cover (precoverage P))
    [Category ğ’°.Iâ‚€] [ğ’°.LocallyDirected] {Y : Scheme.{u}} (f : Y âŸ¶ X)

instance : Category (ğ’°.pullbackâ‚ f).Iâ‚€ := inferInstanceAs <| Category ğ’°.Iâ‚€

instance locallyDirectedPullbackCover : Cover.LocallyDirected (ğ’°.pullbackâ‚ f) where
  trans {i j} hij := pullback.map f (ğ’°.f i) f (ğ’°.f j) (ğŸ™ _) (ğ’°.trans hij) (ğŸ™ _)
    (by simp) (by simp)
  trans_id i := by simp
  trans_comp hij hjk := by simp [pullback.map_comp]
  directed {i j} x := by
    dsimp at i j x âŠ¢
    let iso : pullback (pullback.fst f (ğ’°.f i)) (pullback.fst f (ğ’°.f j)) â‰…
        pullback f (pullback.fst (ğ’°.f i) (ğ’°.f j) â‰« ğ’°.f i) :=
      pullbackRightPullbackFstIso _ _ _ â‰ªâ‰« pullback.congrHom pullback.condition rfl â‰ªâ‰«
        pullbackAssoc ..
    have (k : ğ’°.Iâ‚€) (hki : k âŸ¶ i) (hkj : k âŸ¶ j) :
        (pullback.lift
          (pullback.map f (ğ’°.f k) f (ğ’°.f i) (ğŸ™ Y) (ğ’°.trans hki) (ğŸ™ X) (by simp) (by simp))
          (pullback.map f (ğ’°.f k) f (ğ’°.f j) (ğŸ™ Y) (ğ’°.trans hkj) (ğŸ™ X) (by simp) (by simp))
            (by simp)) =
          pullback.map _ _ _ _ (ğŸ™ Y) (pullback.lift (ğ’°.trans hki) (ğ’°.trans hkj) (by simp)) (ğŸ™ X)
            (by simp) (by simp) â‰« iso.inv := by
      apply pullback.hom_ext <;> apply pullback.hom_ext <;> simp [iso]
    obtain âŸ¨k, hki, hkj, yk, hykâŸ© := ğ’°.exists_lift_trans_eq ((iso.hom â‰« pullback.snd _ _).base x)
    refine âŸ¨k, hki, hkj, show x âˆˆ Set.range _ from ?_âŸ©
    rw [this, Scheme.Hom.comp_base, TopCat.coe_comp, Set.range_comp, Pullback.range_map]
    use iso.hom.base x
    simp only [Hom.id_base, TopCat.hom_id, ContinuousMap.coe_id, Set.range_id, Set.preimage_univ,
      Set.univ_inter, Set.mem_preimage, Set.mem_range, hom_inv_apply, and_true]
    exact âŸ¨yk, hykâŸ©
  property_trans {i j} hij := by
    let iso : pullback f (ğ’°.f i) â‰… pullback (pullback.snd f (ğ’°.f j)) (ğ’°.trans hij) :=
      pullback.congrHom rfl (by simp) â‰ªâ‰« (pullbackLeftPullbackSndIso _ _ _).symm
    rw [â† P.cancel_left_of_respectsIso iso.inv]
    simp [Iso.trans_inv, Iso.symm_inv, pullback.congrHom_inv,
      Category.assoc, iso]
    convert P.pullback_fst _ _ (ğ’°.property_trans hij)
    apply pullback.hom_ext <;> simp [pullback.condition]

end BaseChange

end Cover

namespace OpenCover

variable (ğ’° : X.OpenCover) [Category ğ’°.Iâ‚€] [ğ’°.LocallyDirected]

instance {i j : ğ’°.Iâ‚€} (f : i âŸ¶ j) : IsOpenImmersion (ğ’°.trans f) :=
  ğ’°.property_trans f

instance {i j : ğ’°.Iâ‚€} (f : i âŸ¶ j) : IsOpenImmersion (ğ’°.functorOfLocallyDirected.map f) :=
  ğ’°.property_trans f

/--
If `ğ’°` is a directed open cover of `X`, to glue morphisms `{gáµ¢ : ğ’°áµ¢ âŸ¶ Y}` it suffices
to check compatibility with the transition maps.
See `OpenCover.isColimitCoconeOfLocallyDirected` for this result stated in the language of
colimits.
-/
def glueMorphismsOfLocallyDirected (ğ’° : X.OpenCover) [Category ğ’°.Iâ‚€] [ğ’°.LocallyDirected]
    {Y : Scheme.{u}}
    (g : âˆ€ i, ğ’°.X i âŸ¶ Y) (h : âˆ€ {i j : ğ’°.Iâ‚€} (hij : i âŸ¶ j), ğ’°.trans hij â‰« g j = g i) :
    X âŸ¶ Y :=
  ğ’°.glueMorphisms g <| fun i j â†¦ by
    apply (ğ’°.intersectionOfLocallyDirected i j).hom_ext
    intro k
    simp [h]

@[reassoc (attr := simp)]
lemma map_glueMorphismsOfLocallyDirected {Y : Scheme.{u}} (g : âˆ€ i, ğ’°.X i âŸ¶ Y)
    (h : âˆ€ {i j : ğ’°.Iâ‚€} (hij : i âŸ¶ j), ğ’°.trans hij â‰« g j = g i) (i : ğ’°.Iâ‚€) :
    ğ’°.f i â‰« ğ’°.glueMorphismsOfLocallyDirected g h = g i := by
  simp [glueMorphismsOfLocallyDirected]

/-- If `ğ’°` is an open cover of `X` that is locally directed, `X` is
the colimit of the components of `ğ’°`. -/
def isColimitCoconeOfLocallyDirected : IsColimit ğ’°.coconeOfLocallyDirected where
  desc s := ğ’°.glueMorphismsOfLocallyDirected s.Î¹.app fun _ â†¦ s.Î¹.naturality _
  uniq s m hm := ğ’°.hom_ext _ _ fun j â†¦ by simpa using hm j

/-- If `ğ’°` is a directed open cover of `X`, to glue morphisms `{gáµ¢ : ğ’°áµ¢ âŸ¶ Y}` over `S` it suffices
to check compatibility with the transition maps. -/
def glueMorphismsOverOfLocallyDirected {S : Scheme.{u}} {X : Over S}
    (ğ’° : X.left.OpenCover) [Category ğ’°.Iâ‚€] [ğ’°.LocallyDirected] {Y : Over S}
    (g : âˆ€ i, ğ’°.X i âŸ¶ Y.left)
    (h : âˆ€ {i j : ğ’°.Iâ‚€} (hij : i âŸ¶ j), ğ’°.trans hij â‰« g j = g i)
    (w : âˆ€ i, g i â‰« Y.hom = ğ’°.f i â‰« X.hom) :
    X âŸ¶ Y :=
  Over.homMk (ğ’°.glueMorphismsOfLocallyDirected g h) <| by
    apply ğ’°.hom_ext
    intro i
    simp [w]

@[reassoc (attr := simp)]
lemma map_glueMorphismsOverOfLocallyDirected_left {S : Scheme.{u}} {X : Over S}
    (ğ’° : X.left.OpenCover) [Category ğ’°.Iâ‚€] [ğ’°.LocallyDirected] {Y : Over S}
    (g : âˆ€ i, ğ’°.X i âŸ¶ Y.left) (h : âˆ€ {i j : ğ’°.Iâ‚€} (hij : i âŸ¶ j), ğ’°.trans hij â‰« g j = g i)
    (w : âˆ€ i, g i â‰« Y.hom = ğ’°.f i â‰« X.hom) (i : ğ’°.Iâ‚€) :
    ğ’°.f i â‰« (ğ’°.glueMorphismsOverOfLocallyDirected g h w).left = g i := by
  simp [glueMorphismsOverOfLocallyDirected]

end OpenCover

/-- If `ğ’°` is an open cover such that the images of the components form a basis of the topology
of `X`, `ğ’°` is directed by the ordering of subset inclusion of the images. -/
def Cover.LocallyDirected.ofIsBasisOpensRange {ğ’° : X.OpenCover} [Preorder ğ’°.Iâ‚€]
    (hle : âˆ€ {i j : ğ’°.Iâ‚€}, i â‰¤ j â†” (ğ’°.f i).opensRange â‰¤ (ğ’°.f j).opensRange)
    (H : TopologicalSpace.Opens.IsBasis (Set.range <| fun i â†¦ (ğ’°.f i).opensRange)) :
    ğ’°.LocallyDirected where
  trans {i j} hij := IsOpenImmersion.lift (ğ’°.f j) (ğ’°.f i) (hle.mp (leOfHom hij))
  trans_id i := by rw [â† cancel_mono (ğ’°.f i)]; simp
  trans_comp hij hjk := by rw [â† cancel_mono (ğ’°.f _)]; simp
  directed {i j} x := by
    have : (pullback.fst (ğ’°.f i) (ğ’°.f j) â‰« ğ’°.f i).base x âˆˆ
      (pullback.fst (ğ’°.f i) (ğ’°.f j) â‰« ğ’°.f i).opensRange := âŸ¨x, rflâŸ©
    obtain âŸ¨k, âŸ¨k, rflâŸ©, âŸ¨y, hyâŸ©, hâŸ© := TopologicalSpace.Opens.isBasis_iff_nbhd.mp H this
    refine âŸ¨k, homOfLE <| hle.mpr <| le_trans h ?_, homOfLE <| hle.mpr <| le_trans h ?_, y, ?_âŸ©
    Â· rw [Scheme.Hom.opensRange_comp]
      exact Set.image_subset_range _ _
    Â· simp_rw [pullback.condition, Scheme.Hom.opensRange_comp]
      exact Set.image_subset_range _ _
    Â· apply (pullback.fst (ğ’°.f i) (ğ’°.f j) â‰« ğ’°.f i).isOpenEmbedding.injective
      rw [â† Scheme.Hom.comp_apply, pullback.lift_fst_assoc, IsOpenImmersion.lift_fac, hy]

section Constructions

section

variable {ğ’° : X.OpenCover} [Preorder ğ’°.Iâ‚€]
  (hle : âˆ€ {i j : ğ’°.Iâ‚€}, i â‰¤ j â†” (ğ’°.f i).opensRange â‰¤ (ğ’°.f j).opensRange)
  (H : TopologicalSpace.Opens.IsBasis (Set.range <| fun i â†¦ (ğ’°.f i).opensRange))

include hle in
lemma Cover.LocallyDirected.ofIsBasisOpensRange_le_iff (i j : ğ’°.Iâ‚€) :
    letI := Cover.LocallyDirected.ofIsBasisOpensRange hle H
    i â‰¤ j â†” (ğ’°.f i).opensRange â‰¤ (ğ’°.f j).opensRange := hle

lemma Cover.LocallyDirected.ofIsBasisOpensRange_trans {i j : ğ’°.Iâ‚€} :
    letI := Cover.LocallyDirected.ofIsBasisOpensRange hle H
    (hij : i â‰¤ j) â†’ ğ’°.trans (homOfLE hij) = IsOpenImmersion.lift (ğ’°.f j) (ğ’°.f i) (hle.mp hij) :=
  fun _ â†¦ rfl

end

variable (X) in
open TopologicalSpace.Opens in
/-- The directed affine open cover of `X` given by all affine opens. -/
@[simps Iâ‚€ X f]
def directedAffineCover : X.OpenCover where
  Iâ‚€ := X.affineOpens
  X U := U
  f U := U.1.Î¹
  memâ‚€ := by
    rw [presieveâ‚€_mem_precoverage_iff]
    refine âŸ¨fun x â†¦ ?_, inferInstanceâŸ©
    use âŸ¨(isBasis_iff_nbhd.mp X.isBasis_affineOpens (mem_top x)).choose,
      (isBasis_iff_nbhd.mp X.isBasis_affineOpens (mem_top x)).choose_spec.1âŸ©
    simpa using (isBasis_iff_nbhd.mp X.isBasis_affineOpens (mem_top x)).choose_spec.2.1

instance : Preorder X.directedAffineCover.Iâ‚€ := inferInstanceAs <| Preorder X.affineOpens

instance : Scheme.Cover.LocallyDirected X.directedAffineCover :=
  .ofIsBasisOpensRange (by simp) <| by
    convert X.isBasis_affineOpens
    simp

@[simp]
lemma directedAffineCover_trans {U V : X.affineOpens} (hUV : U â‰¤ V) :
    Cover.trans X.directedAffineCover (homOfLE hUV) = X.homOfLE hUV := rfl

end Constructions

end AlgebraicGeometry.Scheme
