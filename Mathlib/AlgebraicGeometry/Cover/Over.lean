/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.AlgebraicGeometry.Morphisms.UnderlyingMap
import Mathlib.CategoryTheory.Limits.MorphismProperty

/-!

# Covers of schemes over a base

In this file we define the typeclass `Cover.Over`. For a cover `ğ’°` of an `S`-scheme `X`,
the datum `ğ’°.Over S` contains `S`-scheme structures on the components of `ğ’°` and asserts
that the component maps are morphisms of `S`-schemes.

We provide instances of `ğ’°.Over S` for standard constructions on covers.

-/

universe v u

noncomputable section

open CategoryTheory Limits

namespace AlgebraicGeometry.Scheme

variable {P : MorphismProperty Scheme.{u}} (S : Scheme.{u})

/-- Bundle an `S`-scheme with `P` into an object of `P.Over âŠ¤ S`. -/
abbrev asOverProp (X : Scheme.{u}) (S : Scheme.{u}) [X.Over S] (h : P (X â†˜ S)) : P.Over âŠ¤ S :=
  âŸ¨X.asOver S, hâŸ©

/-- Bundle an `S`-morphism of `S`-scheme with `P` into a morphism in `P.Over âŠ¤ S`. -/
abbrev Hom.asOverProp {X Y : Scheme.{u}} (f : X.Hom Y) (S : Scheme.{u}) [X.Over S] [Y.Over S]
    [f.IsOver S] {hX : P (X â†˜ S)} {hY : P (Y â†˜ S)} : X.asOverProp S hX âŸ¶ Y.asOverProp S hY :=
  âŸ¨f.asOver S, trivial, trivialâŸ©

/-- A `P`-cover of a scheme `X` over `S` is a cover, where the components are over `S` and the
component maps commute with the structure morphisms. -/
protected class Cover.Over {P : MorphismProperty Scheme.{u}} [P.IsStableUnderBaseChange]
    [IsJointlySurjectivePreserving P] {X : Scheme.{u}} [X.Over S]
    (ğ’° : X.Cover (precoverage P)) where
  over (j : ğ’°.Iâ‚€) : (ğ’°.X j).Over S := by infer_instance
  isOver_map (j : ğ’°.Iâ‚€) : (ğ’°.f j).IsOver S := by infer_instance

attribute [instance] Cover.Over.over Cover.Over.isOver_map

variable [P.IsStableUnderBaseChange] [IsJointlySurjectivePreserving P]

instance [P.ContainsIdentities] [P.RespectsIso] {X Y : Scheme.{u}} (f : X âŸ¶ Y) [X.Over S] [Y.Over S]
    [f.IsOver S] [IsIso f] : (coverOfIsIso (P := P) f).Over S where
  over _ := inferInstanceAs <| X.Over S
  isOver_map _ := inferInstanceAs <| f.IsOver S

section

variable {X W : Scheme.{u}} (ğ’° : X.Cover (precoverage P)) (f : W âŸ¶ X) [W.Over S] [X.Over S]
  [ğ’°.Over S] [f.IsOver S]

/-- The pullback of a cover of `S`-schemes along a morphism of `S`-schemes. This is not
definitionally equal to `AlgebraicGeometry.Scheme.Cover.pullbackâ‚`, as here we take
the pullback in `Over S`, whose underlying scheme is only isomorphic but not equal to the
pullback in `Scheme`. -/
@[simps]
def Cover.pullbackCoverOver : W.Cover (precoverage P) where
  Iâ‚€ := ğ’°.Iâ‚€
  X x := (pullback (f.asOver S) ((ğ’°.f x).asOver S)).left
  f x := (pullback.fst (f.asOver S) ((ğ’°.f x).asOver S)).left
  memâ‚€ := by
    rw [presieveâ‚€_mem_precoverage_iff]
    refine âŸ¨fun x â†¦ ?_, fun j â†¦ ?_âŸ©
    Â· obtain âŸ¨i, hyâŸ© := Cover.exists_eq (ğ’°.pullbackâ‚ f) x
      use i
      exact (mem_range_iff_of_surjective ((ğ’°.pullbackâ‚ f).f i) _
        ((PreservesPullback.iso (Over.forget S) (f.asOver S) ((ğ’°.f _).asOver S)).inv)
        (PreservesPullback.iso_inv_fst _ _ _) x).mp hy
    Â· dsimp only
      rw [â† Over.forget_map, â† PreservesPullback.iso_hom_fst, P.cancel_left_of_respectsIso]
      exact P.pullback_fst _ _ (ğ’°.map_prop j)

instance (j : ğ’°.Iâ‚€) : ((ğ’°.pullbackCoverOver S f).X j).Over S where
  hom := (pullback (f.asOver S) ((ğ’°.f j).asOver S)).hom

instance : (ğ’°.pullbackCoverOver S f).Over S where
  isOver_map j := { comp_over := by exact Over.w (pullback.fst (f.asOver S) ((ğ’°.f j).asOver S)) }

/-- A variant of `AlgebraicGeometry.Scheme.Cover.pullbackCoverOver` with the arguments in the
fiber products flipped. -/
@[simps]
def Cover.pullbackCoverOver' : W.Cover (precoverage P) where
  Iâ‚€ := ğ’°.Iâ‚€
  X x := (pullback ((ğ’°.f x).asOver S) (f.asOver S)).left
  f x := (pullback.snd ((ğ’°.f x).asOver S) (f.asOver S)).left
  memâ‚€ := by
    rw [presieveâ‚€_mem_precoverage_iff]
    refine âŸ¨fun x â†¦ ?_, fun j â†¦ ?_âŸ©
    Â· obtain âŸ¨i, hyâŸ© := Cover.exists_eq (ğ’°.pullbackâ‚‚ f) x
      use i
      exact (mem_range_iff_of_surjective ((ğ’°.pullbackâ‚‚ f).f _) _
        ((PreservesPullback.iso (Over.forget S) ((ğ’°.f _).asOver S) (f.asOver S)).inv)
        (PreservesPullback.iso_inv_snd _ _ _) x).mp hy
    Â· dsimp only
      rw [â† Over.forget_map, â† PreservesPullback.iso_hom_snd, P.cancel_left_of_respectsIso]
      exact P.pullback_snd _ _ (ğ’°.map_prop j)

instance (j : ğ’°.Iâ‚€) : ((ğ’°.pullbackCoverOver' S f).X j).Over S where
  hom := (pullback ((ğ’°.f j).asOver S) (f.asOver S)).hom

instance : (ğ’°.pullbackCoverOver' S f).Over S where
  isOver_map j := { comp_over := by exact Over.w (pullback.snd ((ğ’°.f j).asOver S) (f.asOver S)) }

variable {Q : MorphismProperty Scheme.{u}} [Q.HasOfPostcompProperty Q]
  [Q.IsStableUnderBaseChange] [Q.IsStableUnderComposition]

variable (hX : Q (X â†˜ S)) (hW : Q (W â†˜ S)) (hQ : âˆ€ j, Q (ğ’°.X j â†˜ S))

/-- The pullback of a cover of `S`-schemes with `Q` along a morphism of `S`-schemes. This is not
definitionally equal to `AlgebraicGeometry.Scheme.Cover.pullbackCover`, as here we take
the pullback in `Q.Over âŠ¤ S`, whose underlying scheme is only isomorphic but not equal to the
pullback in `Scheme`. -/
@[simps -isSimp]
def Cover.pullbackCoverOverProp : W.Cover (precoverage P) where
  Iâ‚€ := ğ’°.Iâ‚€
  X x := (pullback (f.asOverProp (hX := hW) (hY := hX) S)
    ((ğ’°.f x).asOverProp (hX := hQ x) (hY := hX) S)).left
  f x := (pullback.fst (f.asOverProp S) ((ğ’°.f x).asOverProp S)).left
  memâ‚€ := by
    rw [presieveâ‚€_mem_precoverage_iff]
    refine âŸ¨fun x â†¦ ?_, fun j â†¦ ?_âŸ©
    Â· obtain âŸ¨i, hyâŸ© := Cover.exists_eq (ğ’°.pullbackâ‚ f) x
      use i
      exact (mem_range_iff_of_surjective ((ğ’°.pullbackâ‚ f).f i) _
        ((PreservesPullback.iso (MorphismProperty.Over.forget Q _ _ â‹™ Over.forget S)
          (f.asOverProp S) ((ğ’°.f _).asOverProp S)).inv)
        (PreservesPullback.iso_inv_fst _ _ _) x).mp hy
    Â· dsimp only
      rw [â† Over.forget_map, MorphismProperty.Comma.toCommaMorphism_eq_hom,
        â† MorphismProperty.Comma.forget_map, â† Functor.comp_map]
      rw [â† PreservesPullback.iso_hom_fst, P.cancel_left_of_respectsIso]
      exact P.pullback_fst _ _ (ğ’°.map_prop j)

instance (j : ğ’°.Iâ‚€) : ((ğ’°.pullbackCoverOverProp S f hX hW hQ).X j).Over S where
  hom := (pullback (f.asOverProp (hX := hW) (hY := hX) S)
    ((ğ’°.f j).asOverProp (hX := hQ j) (hY := hX) S)).hom

instance : (ğ’°.pullbackCoverOverProp S f hX hW hQ).Over S where
  isOver_map j :=
    { comp_over := by exact (pullback.fst (f.asOverProp S) ((ğ’°.f j).asOverProp S)).w }

/-- A variant of `AlgebraicGeometry.Scheme.Cover.pullbackCoverOverProp` with the arguments in the
fiber products flipped. -/
@[simps -isSimp]
def Cover.pullbackCoverOverProp' : W.Cover (precoverage P) where
  Iâ‚€ := ğ’°.Iâ‚€
  X x := (pullback ((ğ’°.f x).asOverProp (hX := hQ x) (hY := hX) S)
    (f.asOverProp (hX := hW) (hY := hX) S)).left
  f x := (pullback.snd ((ğ’°.f x).asOverProp S) (f.asOverProp S)).left
  memâ‚€ := by
    rw [presieveâ‚€_mem_precoverage_iff]
    refine âŸ¨fun x â†¦ ?_, fun j â†¦ ?_âŸ©
    Â· obtain âŸ¨i, hyâŸ© := Cover.exists_eq (ğ’°.pullbackâ‚‚ f) x
      use i
      exact (mem_range_iff_of_surjective ((ğ’°.pullbackâ‚‚ f).f i) _
        ((PreservesPullback.iso (MorphismProperty.Over.forget Q _ _ â‹™ Over.forget S)
          ((ğ’°.f _).asOverProp S) (f.asOverProp S)).inv)
        (PreservesPullback.iso_inv_snd _ _ _) x).mp hy
    Â· dsimp only
      rw [â† Over.forget_map, MorphismProperty.Comma.toCommaMorphism_eq_hom,
        â† MorphismProperty.Comma.forget_map, â† Functor.comp_map]
      rw [â† PreservesPullback.iso_hom_snd, P.cancel_left_of_respectsIso]
      exact P.pullback_snd _ _ (ğ’°.map_prop j)

instance (j : ğ’°.Iâ‚€) : ((ğ’°.pullbackCoverOverProp' S f hX hW hQ).X j).Over S where
  hom := (pullback ((ğ’°.f j).asOverProp (hX := hQ j) (hY := hX) S)
    (f.asOverProp (hX := hW) (hY := hX) S)).hom

instance : (ğ’°.pullbackCoverOverProp' S f hX hW hQ).Over S where
  isOver_map j :=
    { comp_over := by exact (pullback.snd ((ğ’°.f j).asOverProp S) (f.asOverProp S)).w }

end

variable [P.IsStableUnderComposition]
variable {X : Scheme.{u}} (ğ’° : X.Cover (precoverage P)) (ğ’± : âˆ€ x, (ğ’°.X x).Cover (precoverage P))
  [X.Over S] [ğ’°.Over S] [âˆ€ x, (ğ’± x).Over S]

instance (j : (ğ’°.bind ğ’±).Iâ‚€) : ((ğ’°.bind ğ’±).X j).Over S :=
  inferInstanceAs <| ((ğ’± j.1).X j.2).Over S

instance {X : Scheme.{u}} (ğ’° : X.Cover (precoverage P)) (ğ’± : âˆ€ x, (ğ’°.X x).Cover (precoverage P))
    [X.Over S] [ğ’°.Over S] [âˆ€ x, (ğ’± x).Over S] : Cover.Over S (ğ’°.bind ğ’±) where
  over := fun âŸ¨i, jâŸ© â†¦ inferInstanceAs <| ((ğ’± i).X j).Over S
  isOver_map := fun âŸ¨i, jâŸ© â†¦ { comp_over := by simp }

end AlgebraicGeometry.Scheme
