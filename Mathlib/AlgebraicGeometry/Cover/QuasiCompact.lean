/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
module

public import Mathlib.AlgebraicGeometry.Morphisms.Affine
public import Mathlib.AlgebraicGeometry.Properties
public import Mathlib.AlgebraicGeometry.PullbackCarrier
public import Mathlib.Topology.Sets.CompactOpenCovered

/-!
# Quasi-compact covers

A cover of a scheme is quasi-compact if every affine open of the base can be covered
by a finite union of images of quasi-compact opens of the components.

This is used to define the fpqc (faithfully flat, quasi-compact) topology, where covers are given by
flat covers that are quasi-compact.
-/

@[expose] public section

universe w' w u v

open CategoryTheory Limits MorphismProperty TopologicalSpace.Opens AlgebraicGeometry

namespace AlgebraicGeometry

variable {S : Scheme.{u}}

/--
A cover of a scheme is quasi-compact if every affine open of the base can be covered
by a finite union of images of quasi-compact opens of the components.
-/
@[stacks 022B, mk_iff]
class QuasiCompactCover (ğ’° : PreZeroHypercover.{v} S) : Prop where
  isCompactOpenCovered_of_isAffineOpen {U : S.Opens} (hU : IsAffineOpen U) :
    IsCompactOpenCovered (ğ’°.f Â·) (U : Set S)

variable (ğ’° : PreZeroHypercover.{v} S)

lemma IsAffineOpen.isCompactOpenCovered [QuasiCompactCover ğ’°] {U : S.Opens} (hU : IsAffineOpen U) :
    IsCompactOpenCovered (ğ’°.f Â·) (U : Set S) :=
  QuasiCompactCover.isCompactOpenCovered_of_isAffineOpen hU

namespace QuasiCompactCover

lemma isCompactOpenCovered_of_isCompact [QuasiCompactCover ğ’°]
    {U : S.Opens} (hU : IsCompact (U : Set S)) :
    IsCompactOpenCovered (ğ’°.f Â·) (U : Set S) := by
  obtain âŸ¨Us, hUs, hUf, hUcâŸ© := S.isBasis_affineOpens.exists_finite_of_isCompact hU
  refine .of_biUnion_eq_of_finite (SetLike.coe '' Us) (by aesop) (hUf.image _) ?_
  simpa using fun t ht â†¦ IsAffineOpen.isCompactOpenCovered ğ’° (hUs ht)

variable {ğ’° : PreZeroHypercover.{v} S} {K : Precoverage Scheme.{u}}

variable (ğ’°) in
lemma exists_isAffineOpen_of_isCompact [QuasiCompactCover ğ’°] {U : S.Opens}
    (hU : IsCompact (U : Set S)) :
    âˆƒ (n : â„•) (f : Fin n â†’ ğ’°.Iâ‚€) (V : âˆ€ i, (ğ’°.X (f i)).Opens),
      (âˆ€ i, IsAffineOpen (V i)) âˆ§
      â‹ƒ i, ğ’°.f (f i) '' (V i) = U := by
  obtain âŸ¨n, a, V, ha, heqâŸ© := (isCompactOpenCovered_of_isCompact ğ’° hU).exists_mem_of_isBasis
    (fun i â†¦ (ğ’°.X i).isBasis_affineOpens) (fun _ _ h â†¦ h.isCompact)
  exact âŸ¨n, a, V, ha, heqâŸ©

/-- If the component maps of `ğ’°` are open, `ğ’°` is quasi-compact. This in particular
applies if `K` is the fppf topology (i.e., flat and of finite presentation) and hence in
particular for Ã©tale and Zariski covers. -/
@[stacks 022C]
lemma of_isOpenMap {ğ’° : S.Cover K} [Scheme.JointlySurjective K] (h : âˆ€ i, IsOpenMap (ğ’°.f i)) :
    QuasiCompactCover ğ’°.toPreZeroHypercover where
  isCompactOpenCovered_of_isAffineOpen {U} hU := .of_isOpenMap
    (fun i â†¦ (ğ’°.f i).continuous) h (fun x _ â†¦ âŸ¨ğ’°.idx x, ğ’°.covers xâŸ©) U.2 hU.isCompact

/-- Any open cover is quasi-compact. -/
instance (ğ’° : S.OpenCover) : QuasiCompactCover ğ’°.toPreZeroHypercover :=
  of_isOpenMap fun i â†¦ (ğ’°.f i).isOpenEmbedding.isOpenMap

/-- If `ğ’±` is a refinement of `ğ’°` such that `ğ’±` is quasicompact, also `ğ’°` is quasicompact. -/
@[stacks 03L8]
lemma of_hom {ğ’± : PreZeroHypercover.{w'} S} (f : ğ’±.Hom ğ’°) [QuasiCompactCover ğ’±] :
    QuasiCompactCover ğ’° := by
  refine âŸ¨fun {U} hU â†¦ ?_âŸ©
  exact .of_comp (a := f.sâ‚€) (ğ’±.f Â·) (f.hâ‚€ Â·)
    (fun _ â†¦ Scheme.Hom.continuous _) (fun i â†¦ funext <| by simp [â† Scheme.Hom.comp_apply])
    (fun _ â†¦ Scheme.Hom.continuous _) U.2 (hU.isCompactOpenCovered ğ’±)

variable (ğ’°) in
@[stacks 022D "(3)"]
instance [QuasiCompactCover ğ’°] {T : Scheme.{u}} (f : T âŸ¶ S) :
    QuasiCompactCover (ğ’°.pullbackâ‚ f) := by
  refine âŸ¨fun {U'} hU' â†¦ ?_âŸ©
  wlog h : âˆƒ (U : S.Opens), IsAffineOpen U âˆ§ f '' U' âŠ† U generalizing U'
  Â· refine .of_isCompact_of_forall_exists_isCompactOpenCovered hU'.isCompact fun x hxU â†¦ ?_
    obtain âŸ¨W, hW, hx, _âŸ© := isBasis_iff_nbhd.mp S.isBasis_affineOpens (mem_top (f x))
    obtain âŸ¨W', hW', hx', hleâŸ© := isBasis_iff_nbhd.mp T.isBasis_affineOpens
      (show x âˆˆ f â»Â¹áµ W âŠ“ U' from âŸ¨hx, hxUâŸ©)
    exact âŸ¨W', le_trans hle inf_le_right, by simpa [hx], W'.2,
      this hW' âŸ¨W, hW, by simpa using le_trans hle inf_le_leftâŸ©âŸ©
  obtain âŸ¨U, hU, hsubâŸ© := h
  obtain âŸ¨s, hf, V, hc, (heq : _ = (U : Set S))âŸ© := hU.isCompactOpenCovered ğ’°
  refine âŸ¨s, hf, fun i hi â†¦ pullback.fst f (ğ’°.f i) â»Â¹áµ U' âŠ“ pullback.snd f (ğ’°.f i) â»Â¹áµ (V i hi),
      fun i hi â†¦ ?_, ?_âŸ©
  Â· exact hU'.isCompact_pullback_inf (hc _ _) hU (by simpa using hsub) <| show _ âŠ† _ by
      simpa [â† heq, Set.range_comp] using Set.subset_iUnion_of_subset i
        (Set.subset_iUnion_of_subset hi (Set.subset_preimage_image _ _))
  Â· refine subset_antisymm (by simp) (fun x hx â†¦ ?_)
    have : f x âˆˆ (U : Set S) := hsub âŸ¨x, hx, rflâŸ©
    simp_rw [â† heq, Set.mem_iUnion] at this
    obtain âŸ¨i, hi, y, hy, heqâŸ© := this
    simp_rw [Set.mem_iUnion]
    obtain âŸ¨z, hzl, hzrâŸ© := Scheme.Pullback.exists_preimage_pullback x y heq.symm
    exact âŸ¨i, hi, z, âŸ¨by simpa [hzl], by simpa [hzr]âŸ©, hzlâŸ©

variable (ğ’°) in
instance [QuasiCompactCover ğ’°] {T : Scheme.{u}} (f : T âŸ¶ S) :
    QuasiCompactCover (ğ’°.pullbackâ‚‚ f) :=
  .of_hom (PreZeroHypercover.pullbackIso f ğ’°).hom

@[stacks 022D "(2)"]
instance {X : Scheme.{u}} (ğ’° : PreZeroHypercover.{w} X) [QuasiCompactCover ğ’°]
    (f : âˆ€ (x : ğ’°.Iâ‚€), PreZeroHypercover.{w} (ğ’°.X x)) [âˆ€ x, QuasiCompactCover (f x)] :
    QuasiCompactCover (ğ’°.bind f) where
  isCompactOpenCovered_of_isAffineOpen {U} hU := by
    obtain âŸ¨s, hs, V, hcV, hUâŸ© := hU.isCompactOpenCovered ğ’°
    have (i) (hi) : IsCompactOpenCovered ((f i).f Â·) (V i hi) :=
      isCompactOpenCovered_of_isCompact (f i) (hcV i hi)
    choose t ht W hcW hV using this
    have : Finite s := hs
    have (i) (hi) : Finite (t i hi) := ht i hi
    refine .of_finite (Îº := Î£ (i : s), t i.1 i.2) (fun p â†¦ âŸ¨p.1, p.2âŸ©) (fun p â†¦ W _ p.1.2 _ p.2.2)
      (fun p â†¦ hcW ..) ?_
    simpa [â† hV, Set.iUnion_sigma, Set.iUnion_subtype, Set.image_iUnion, Set.image_image] using hU

instance of_finite {ğ’° : S.Cover K} [Scheme.JointlySurjective K]
    [âˆ€ i, AlgebraicGeometry.QuasiCompact (ğ’°.f i)] [Finite ğ’°.Iâ‚€] :
    QuasiCompactCover ğ’°.toPreZeroHypercover where
  isCompactOpenCovered_of_isAffineOpen {U} hU := by
    refine .of_finite_of_isSpectralMap (fun i â†¦ (ğ’°.f i).isSpectralMap) ?_ U.2 hU.isCompact
    exact (fun x _ â†¦ âŸ¨ğ’°.idx x, ğ’°.covers xâŸ©)

variable {P : MorphismProperty Scheme.{u}}

instance homCover {X S : Scheme.{u}} (f : X âŸ¶ S) (hf : P f) [Surjective f]
    [AlgebraicGeometry.QuasiCompact f] : QuasiCompactCover (f.cover hf).toPreZeroHypercover :=
  have _ (i) : AlgebraicGeometry.QuasiCompact ((f.cover hf).f i) := â€¹_â€º
  .of_finite

instance singleton {X : Scheme.{u}} (f : X âŸ¶ S) [Surjective f]
    [AlgebraicGeometry.QuasiCompact f] :
    QuasiCompactCover (.singleton f) :=
  homCover (P := âŠ¤) f trivial

@[stacks 022D "(1)"]
instance {P : MorphismProperty Scheme.{u}} [P.ContainsIdentities] [P.RespectsIso]
    {X Y : Scheme.{u}} {f : X âŸ¶ Y} [IsIso f] :
    QuasiCompactCover (Scheme.coverOfIsIso (P := P) f).toPreZeroHypercover :=
  of_isOpenMap (fun _ â†¦ f.homeomorph.isOpenMap)

end QuasiCompactCover

namespace Scheme

/-- The object property on the category of pre-`0`-hypercovers of a scheme given
by quasi-compact covers. -/
def quasiCompactCover (S : Scheme.{u}) : ObjectProperty (PreZeroHypercover.{v} S) :=
  QuasiCompactCover

@[simp]
lemma quasiCompactCover_iff (S : Scheme.{u}) (ğ’° : PreZeroHypercover.{v} S) :
    S.quasiCompactCover ğ’° â†” QuasiCompactCover ğ’° := .rfl

instance isClosedUnderIsomorphisms_quasiCompactCover (S : Scheme.{u}) :
    S.quasiCompactCover.IsClosedUnderIsomorphisms where
  of_iso {ğ’° _} e (_ : QuasiCompactCover ğ’°) := .of_hom e.hom

end Scheme

end AlgebraicGeometry
