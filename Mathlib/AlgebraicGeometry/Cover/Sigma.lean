/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
module

public import Mathlib.AlgebraicGeometry.Morphisms.Basic

/-!
# Collapsing covers

We define the endofunctor on `Scheme.Cover P` that collapses a cover to a single object cover.
-/

@[expose] public section

universe v u

open CategoryTheory Limits

namespace AlgebraicGeometry.Scheme.Cover

variable {P : MorphismProperty Scheme.{u}} {S : Scheme.{u}} [IsZariskiLocalAtSource P]
  [UnivLE.{v, u}] [P.IsStableUnderBaseChange] [IsJointlySurjectivePreserving P]

/-- If `ğ’°` is a cover of `S`, this is the single object cover where the covering
object is the disjoint union. -/
@[simps]
noncomputable def sigma (ğ’° : Cover.{v} (precoverage P) S) : S.Cover (precoverage P) where
  Iâ‚€ := PUnit.{v + 1}
  X _ := âˆ ğ’°.X
  f _ := Sigma.desc ğ’°.f
  memâ‚€ := by
    rw [presieveâ‚€_mem_precoverage_iff]
    refine âŸ¨fun s â†¦ ?_, fun _ â†¦ IsZariskiLocalAtSource.sigmaDesc ğ’°.map_propâŸ©
    obtain âŸ¨i, y, rflâŸ© := ğ’°.exists_eq s
    refine âŸ¨default, Sigma.Î¹ ğ’°.X i y, by simp [â† Scheme.Hom.comp_apply]âŸ©

variable [P.IsMultiplicative] {ğ’° ğ’± : Scheme.Cover.{v} (precoverage P) S}

variable (ğ’°) in
instance : Unique ğ’°.sigma.Iâ‚€ := inferInstanceAs <| Unique PUnit.{v + 1}

/-- `ğ’°` refines the single object cover defined by `ğ’°`. -/
@[simps]
noncomputable def toSigma (ğ’° : Cover.{v} (precoverage P) S) : ğ’° âŸ¶ ğ’°.sigma where
  sâ‚€ _ := default
  hâ‚€ i := Sigma.Î¹ _ i

/-- A refinement of coverings induces a refinement on the single object coverings. -/
@[simps]
noncomputable def Hom.sigma (f : ğ’° âŸ¶ ğ’±) : ğ’°.sigma âŸ¶ ğ’±.sigma where
  sâ‚€ _ := default
  hâ‚€ _ := Sigma.desc fun j â†¦ f.hâ‚€ j â‰« Sigma.Î¹ _ (f.sâ‚€ j)
  wâ‚€ _ := Sigma.hom_ext _ _ (by simp)

/-- Collapsing a cover to a single object cover is functorial. -/
@[simps]
noncomputable def sigmaFunctor : S.Cover (precoverage P) â¥¤ S.Cover (precoverage P) where
  obj ğ’° := ğ’°.sigma
  map f := Scheme.Cover.Hom.sigma f
  map_id ğ’° := PreZeroHypercover.Hom.ext rfl <| by
    simp only [sigma_Iâ‚€, sigma_X, heq_eq_eq]
    ext j : 1
    exact Sigma.hom_ext _ _ (by simp)
  map_comp f g := PreZeroHypercover.Hom.ext rfl <| by
    simp only [sigma_Iâ‚€, sigma_X, heq_eq_eq]
    ext j : 1
    exact Sigma.hom_ext _ _ (by simp)

end AlgebraicGeometry.Scheme.Cover
