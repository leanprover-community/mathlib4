/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.AlgebraicGeometry.Morphisms.Basic

/-!
# Collapsing covers

We define the endofunctor on `Scheme.Cover P` that collapses a cover to a single object cover.
-/

universe v u

open CategoryTheory Limits

namespace AlgebraicGeometry.Scheme.Cover

variable {P : MorphismProperty Scheme.{u}} {S : Scheme.{u}} [IsZariskiLocalAtSource P]
  [UnivLE.{v, u}] [P.IsStableUnderBaseChange] [IsJointlySurjectivePreserving P]

/-- If `ğ’°` is a cover of `S`, this is the single object cover where the covering
object is the disjoint union. -/
@[simps]
noncomputable def sigma (ğ’° : Cover.{v} (precoverage P) S) : S.Cover (precoverage P) where
  Iâ‚€ := PUnit.{v + 1}
  X _ := âˆ ğ’°.X
  f _ := Sigma.desc ğ’°.f
  memâ‚€ := by
    rw [presieveâ‚€_mem_precoverage_iff]
    refine âŸ¨fun s â†¦ ?_, fun _ â†¦ IsZariskiLocalAtSource.sigmaDesc ğ’°.map_propâŸ©
    obtain âŸ¨i, y, rflâŸ© := ğ’°.exists_eq s
    refine âŸ¨default, (Sigma.Î¹ ğ’°.X i).base y, by simp [â† Scheme.comp_base_apply]âŸ©

variable [P.IsMultiplicative] {ğ’° ğ’± : Scheme.Cover.{v} (precoverage P) S}

variable (ğ’°) in
instance : Unique ğ’°.sigma.Iâ‚€ := inferInstanceAs <| Unique PUnit.{v + 1}

/-- `ğ’°` refines the single object cover defined by `ğ’°`. -/
@[simps]
noncomputable def toSigma (ğ’° : Cover.{v} (precoverage P) S) : ğ’° âŸ¶ ğ’°.sigma where
  idx _ := default
  app i := Sigma.Î¹ _ i
  app_prop _ := IsZariskiLocalAtSource.of_isOpenImmersion _

/-- A refinement of coverings induces a refinement on the single object coverings. -/
@[simps]
noncomputable def Hom.sigma (f : ğ’° âŸ¶ ğ’±) : ğ’°.sigma âŸ¶ ğ’±.sigma where
  idx _ := default
  app _ := Sigma.desc fun j â†¦ f.app j â‰« Sigma.Î¹ _ (f.idx j)
  w _ := Sigma.hom_ext _ _ (by simp)
  app_prop _ := by
    simp only [sigma_X, sigma_Iâ‚€, PUnit.default_eq_unit,
      IsZariskiLocalAtSource.iff_of_openCover (Scheme.IsLocallyDirected.openCover _),
      Discrete.functor_obj_eq_as, IsLocallyDirected.openCover_Iâ‚€, IsLocallyDirected.openCover_X,
      IsLocallyDirected.openCover_f, colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app]
    intro i
    exact P.comp_mem _ _ (f.app_prop i.1) (IsZariskiLocalAtSource.of_isOpenImmersion _)

/-- Collapsing a cover to a single object cover is functorial. -/
@[simps]
noncomputable def sigmaFunctor : S.Cover (precoverage P) â¥¤ S.Cover (precoverage P) where
  obj ğ’° := ğ’°.sigma
  map f := f.sigma
  map_id ğ’° := Scheme.Cover.Hom.ext rfl <| by
    simp only [sigma_Iâ‚€, sigma_X, Hom.sigma_idx, PUnit.default_eq_unit, id_idx_apply, heq_eq_eq]
    ext j : 1
    exact Sigma.hom_ext _ _ (by simp)
  map_comp f g := Scheme.Cover.Hom.ext rfl <| by
    simp only [sigma_Iâ‚€, sigma_X, Hom.sigma_idx, PUnit.default_eq_unit, comp_idx_apply, heq_eq_eq]
    ext j : 1
    exact Sigma.hom_ext _ _ (by simp)

end AlgebraicGeometry.Scheme.Cover
