/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.EllipticCurve.Affine.Basic

/-!

# Singular cubics

Let `E` be a cubic over an arbitrary field `K`,
given by a Weierstrass equation `y¬≤ + a‚ÇÅxy + a‚ÇÉy = x¬≥ + a‚ÇÇx¬≤ + a‚ÇÑx + a‚ÇÜ`.

We define `E.singular` as a typeclass containing the data of a singular point.
(Such a point is unique if exists.
See `WeierstrassCurve.IsSingularPoint.ext` or `WeierstrassCurve.Singular.subsingleton`.)

For curves `E` with `[E.Singular]`, we define `E.SplitSingular` to contain the data of
(one of) the tangents at the singular point.

## TODO (Andrew)
- Classify singular cubics

-/

namespace WeierstrassCurve

variable {R : Type*} [CommRing R] (E : WeierstrassCurve R)

/-- The predicate of being a singular point on an `WeierstrassCurve` in affine coordinates. -/
def Affine.IsSingularPoint (E : WeierstrassCurve.Affine R) (x y : R) : Prop :=
  E.Equation x y ‚àß E.polynomialX.evalEval x y = 0 ‚àß E.polynomialY.evalEval x y = 0

lemma Affine.isSingularPoint_iff {E : WeierstrassCurve.Affine R} {x y : R} :
    E.IsSingularPoint x y ‚Üî E.Equation x y ‚àß
      E.a‚ÇÅ * y - (3 * x ^ 2 + 2 * E.a‚ÇÇ * x + E.a‚ÇÑ) = 0 ‚àß 2 * y + E.a‚ÇÅ * x + E.a‚ÇÉ = 0 := by
  rw [IsSingularPoint, evalEval_polynomialX, evalEval_polynomialY]

lemma Affine.IsSingularPoint.relation_x
    {E : WeierstrassCurve.Affine R} {x y : R} (h : E.IsSingularPoint x y) :
    E.a‚ÇÅ * y = 3 * x ^ 2 + 2 * E.a‚ÇÇ * x + E.a‚ÇÑ :=
  sub_eq_zero.mp ((Affine.isSingularPoint_iff.mp h).2.1)

lemma Affine.IsSingularPoint.relation_y
    {E : WeierstrassCurve.Affine R} {x y : R} (h : E.IsSingularPoint x y) :
    2 * y + E.a‚ÇÅ * x + E.a‚ÇÉ = 0 :=
  (Affine.isSingularPoint_iff.mp h).2.2

variable {E} in
lemma Affine.IsSingularPoint.of_variableChange (f : VariableChange R) {x y}
    (h : (f ‚Ä¢ E).toAffine.IsSingularPoint x y) :
    E.toAffine.IsSingularPoint
      (f.u ^ 2 * x + f.r) (f.u ^ 3 * y + f.u ^ 2 * f.s * x + f.t) := by
  generalize hE' : f ‚Ä¢ E = E' at h
  obtain rfl : E = f‚Åª¬π ‚Ä¢ E' := eq_inv_smul_iff.mpr hE'
  simp only [isSingularPoint_iff, equation_iff', variableChange_inv_a‚ÇÅ, variableChange_inv_a‚ÇÉ,
    variableChange_inv_a‚ÇÇ, variableChange_inv_a‚ÇÑ, variableChange_inv_a‚ÇÜ, toAffine] at h ‚ä¢
  refine ‚ü®?_, ?_, ?_‚ü©
  ¬∑ linear_combination f.u ^ 6 * h.1
  ¬∑ linear_combination - f.u ^ 3 * f.s * h.2.2 + f.u ^ 4 * h.2.1
  ¬∑ linear_combination f.u ^ 3 * h.2.2

variable {E} in
lemma Affine.IsSingularPoint.variableChange (f : VariableChange R) {x y}
    (h : E.toAffine.IsSingularPoint x y) :
    (f ‚Ä¢ E).toAffine.IsSingularPoint
      (f‚Åª¬π.u ^ 2 * x + f‚Åª¬π.r) (f‚Åª¬π.u ^ 3 * y + f‚Åª¬π.u ^ 2 * f‚Åª¬π.s * x + f‚Åª¬π.t) :=
  .of_variableChange f‚Åª¬π (by simpa)

/--
We say that a weierstrass curve is singular if it has at least one **rational** singular point.
We show below that the singular point (if exists) is unique, see `Affine.isSingularPoint_ext`.

Warning:
Because of the rationality assumption, it is not true that every curve that is not
nonsingular (i.e. `IsElliptic`) is `Singular`.
(e.g. `y¬≤ = x¬≥ - T` is not nonsingular over `ùîΩ‚ÇÉ(T)` but the cusp is in a degree `3` extension)

Implementation detail:
This contains data for defeq and computable purposes,
but we do have an `Subsingleton` instance on it.
Theorems who doesn't need the defeq should assume `Nonempty E.Singular` instead.
-/
class Singular (E : WeierstrassCurve R) where
  /-- The x coordinate of the singular point. -/
  x : R
  /-- The y coordinate of the singular point. -/
  y : R
  isSingularPoint (E) : E.toAffine.IsSingularPoint x y

alias singularityX := Singular.x
alias singularityY := Singular.y

instance [E.Singular] : Nonempty E.Singular := Nonempty.intro ‚Äπ_‚Ä∫

lemma isSingularPoint_singularity (E : WeierstrassCurve R) [E.Singular] :
    E.toAffine.IsSingularPoint E.singularityX E.singularityY :=
  Singular.isSingularPoint E

instance (f : VariableChange R) [Nonempty E.Singular] : Nonempty (f ‚Ä¢ E).Singular := by
  let _ : E.Singular := Nonempty.some ‚Äπ_‚Ä∫
  exact ‚ü®_, _, E.isSingularPoint_singularity.variableChange f‚ü©

@[simp]
lemma Œî_of_singular (E : WeierstrassCurve R) [Nonempty E.Singular] : E.Œî = 0 := by
  by_contra h
  obtain ‚ü®x, y, H‚ü© := Nonempty.some ‚Äπ_‚Ä∫
  exact not_and_of_not_or_not (((E.toAffine.equation_iff_nonsingular_of_Œî_ne_zero h).mp H.1).2) H.2

variable {E} in
@[simp]
lemma nonempty_singular_variableChange_iff {f : VariableChange R} :
    Nonempty (f ‚Ä¢ E).Singular ‚Üî Nonempty E.Singular :=
  ‚ü®fun _ ‚Ü¶ by rw [‚Üê inv_smul_smul f E]; infer_instance, fun _ ‚Ü¶ inferInstance‚ü©

/-- We say that a `WeierstrassCurve` is in singular normal form if it is `y¬≤ + a‚ÇÅxy = x¬≥ + a‚ÇÇx¬≤`.
Any weierstrass curve with a (rational) singular point can be turned into this normal form.
See `toIsSingularNF`. -/
class IsSingularNF (E : WeierstrassCurve R) : Prop where
  a‚ÇÉ (E) : E.a‚ÇÉ = 0
  a‚ÇÑ (E) : E.a‚ÇÑ = 0
  a‚ÇÜ (E) : E.a‚ÇÜ = 0

section

variable [E.IsSingularNF]

@[simp] alias a‚ÇÉ_of_isSingularNF := IsSingularNF.a‚ÇÉ
@[simp] alias a‚ÇÑ_of_isSingularNF := IsSingularNF.a‚ÇÑ
@[simp] alias a‚ÇÜ_of_isSingularNF := IsSingularNF.a‚ÇÜ
@[simp] lemma b‚ÇÑ_of_isSingularNF : E.b‚ÇÑ = 0 := by simp [b‚ÇÑ]
@[simp] lemma b‚ÇÜ_of_isSingularNF : E.b‚ÇÜ = 0 := by simp [b‚ÇÜ]
@[simp] lemma b‚Çà_of_isSingularNF : E.b‚Çà = 0 := by simp [b‚Çà]
lemma c‚ÇÑ_of_isSingularNF : E.c‚ÇÑ = E.b‚ÇÇ ^ 2 := by simp [c‚ÇÑ]
lemma c‚ÇÜ_of_isSingularNF : E.c‚ÇÜ = - E.b‚ÇÇ ^ 3 := by simp [c‚ÇÜ]

end

/-- There is an explicit change of variables of a `WeierstrassCurve` to
a singular normal form, provided the existence of a rational singular point. -/
def toIsSingularNF [E.Singular] : VariableChange R where
  u := 1
  r := E.singularityX
  s := 0
  t := E.singularityY

instance toIsSingularNF_spec [E.Singular] : (E.toIsSingularNF ‚Ä¢ E).IsSingularNF := by
  have := E.isSingularPoint_singularity.variableChange E.toIsSingularNF
  generalize hE' : E.toIsSingularNF ‚Ä¢ E = E' at this ‚ä¢
  have inst : Nonempty E'.Singular := hE' ‚ñ∏ inferInstance
  have hE' : E'.toAffine.IsSingularPoint 0 0 := by
    simpa [toIsSingularNF, VariableChange.inv_def] using this
  have H : E'.a‚ÇÜ = 0 ‚àß E'.a‚ÇÑ = 0 ‚àß E'.a‚ÇÉ = 0 := by simpa [Affine.isSingularPoint_iff] using hE'
  constructor <;> aesop

lemma x_eq_zero_of_isSingularNF [IsReduced R] [E.IsSingularNF] {x y}
    (H : E.toAffine.IsSingularPoint x y) : x = 0 := by
  simp only [toAffine, Affine.isSingularPoint_iff, Affine.equation_iff, a‚ÇÉ_of_isSingularNF,
    zero_mul, add_zero, a‚ÇÑ_of_isSingularNF, a‚ÇÜ_of_isSingularNF] at H
  obtain ‚ü®H, h‚ÇÅ, h‚ÇÇ‚ü© := H
  simpa using show x ^ 3 = 0 by linear_combination 2 * H - x * h‚ÇÅ - y * h‚ÇÇ

lemma y_eq_zero_of_isSingularNF [IsReduced R] [E.IsSingularNF] {x y}
    (H : E.toAffine.IsSingularPoint x y) : y = 0 := by
  simpa [x_eq_zero_of_isSingularNF E H, Affine.equation_iff] using H.1

lemma isSingularPoint_iff_of_isSingularNF [IsReduced R] [E.IsSingularNF] {x y} :
    E.toAffine.IsSingularPoint x y ‚Üî x = 0 ‚àß y = 0 := by
  refine ‚ü®fun H ‚Ü¶ ‚ü®x_eq_zero_of_isSingularNF E H, y_eq_zero_of_isSingularNF E H‚ü©, ?_‚ü©
  rintro ‚ü®rfl, rfl‚ü©
  simp [Affine.isSingularPoint_iff, Affine.equation_iff]

@[simp]
lemma singularityX_of_isSingularNF [IsReduced R] [E.IsSingularNF] [E.Singular] :
    E.singularityX = 0 :=
  x_eq_zero_of_isSingularNF E E.isSingularPoint_singularity

@[simp]
lemma singularityY_of_isSingularNF [IsReduced R] [E.IsSingularNF] [E.Singular] :
    E.singularityY = 0 :=
  y_eq_zero_of_isSingularNF E E.isSingularPoint_singularity

/-- A curve in the singular normal form has `(0, 0)` as the singular point. -/
def Singular.ofIsSingularNF [IsReduced R] [E.IsSingularNF] : E.Singular :=
  ‚ü®0, 0, E.isSingularPoint_iff_of_isSingularNF.mpr (by simp)‚ü©

instance [IsReduced R] [E.IsSingularNF] : Nonempty E.Singular := ‚ü®.ofIsSingularNF E‚ü©

lemma Affine.IsSingularPoint.ext [IsReduced R] {E : Affine R} {x y} {x' y'}
    (H : E.IsSingularPoint x y) (H' : E.IsSingularPoint x' y') :
    x = x' ‚àß y = y' := by
  letI : E.Singular := ‚ü®_, _, H‚ü©
  constructor
  ¬∑ simpa [toIsSingularNF, pow_two, VariableChange.mul_def,
      VariableChange.inv_def, add_eq_zero_iff_eq_neg'] using
        (E.toIsSingularNF ‚Ä¢ E).x_eq_zero_of_isSingularNF (H'.variableChange _)
  ¬∑ simpa [toIsSingularNF, pow_two, VariableChange.mul_def,
      VariableChange.inv_def, add_eq_zero_iff_eq_neg'] using
        (E.toIsSingularNF ‚Ä¢ E).y_eq_zero_of_isSingularNF (H'.variableChange _)

instance Singular.subsingleton [IsReduced R] : Subsingleton E.Singular where
  allEq
  | ‚ü®x, y, H‚ü©, ‚ü®x', y', H'‚ü© => by
    obtain ‚ü®rfl, rfl‚ü© := H.ext H'
    rfl

@[simp]
lemma singularityX_variableChange [IsReduced R]
    (f : VariableChange R) [E.Singular] [(f ‚Ä¢ E).Singular] :
    (f ‚Ä¢ E).singularityX = f‚Åª¬π.u ^ 2 * E.singularityX + f‚Åª¬π.r :=
  ((E.isSingularPoint_singularity.variableChange f).ext
      (f ‚Ä¢ E).isSingularPoint_singularity).1.symm

@[simp]
lemma singularityY_variableChange [IsReduced R]
    (f : VariableChange R) [E.Singular] [(f ‚Ä¢ E).Singular] :
    (f ‚Ä¢ E).singularityY =
      ‚Üëf‚Åª¬π.u ^ 3 * E.singularityY + ‚Üëf‚Åª¬π.u ^ 2 * f‚Åª¬π.s * E.singularityX + f‚Åª¬π.t :=
  ((E.isSingularPoint_singularity.variableChange f).ext
      (f ‚Ä¢ E).isSingularPoint_singularity).2.symm

/-- We say that a weierstrass curve is split singular if it has a (rational) singular point,
and its tangent lines at the singular point is split in the base field.
We later show that this is unique up to swapping the tangents.

Implementation detail:
This contains data for defeq and computable purposes. -/
class SplitSingular [IsReduced R] (E : WeierstrassCurve R) where
  mk' ::
  nonempty_singular : Nonempty E.Singular := by infer_instance
  /-- The slope of (one of) the tangent line at a singular point. -/
  Œ± : R
  cond' (E) : Œ± ^ 2 + E.a‚ÇÅ * Œ± - (E.a‚ÇÇ + 3 * nonempty_singular.some.x) = 0

attribute [instance] SplitSingular.nonempty_singular

/-- The slope of one of the tangent line at a singular point. -/
alias Œ±‚ÇÅ := SplitSingular.Œ±

/-- The slope of the other the tangent line at a singular point. -/
def Œ±‚ÇÇ [IsReduced R] [E.SplitSingular] := - E.a‚ÇÅ - E.Œ±‚ÇÅ

lemma SplitSingular.cond [IsReduced R] (E : WeierstrassCurve R) [E.Singular] [E.SplitSingular] :
    E.Œ±‚ÇÅ ^ 2 + E.a‚ÇÅ * E.Œ±‚ÇÅ - (E.a‚ÇÇ + 3 * E.singularityX) = 0 := by
  convert cond' E
  exact Subsingleton.elim _ _

/-- Constructor of `SplitSingular` assuming we already have a `Singular` instance. -/
def SplitSingular.mk [IsReduced R] (E : WeierstrassCurve R) [E.Singular]
    (Œ± : R) (cond : Œ± ^ 2 + E.a‚ÇÅ * Œ± - (E.a‚ÇÇ + 3 * E.singularityX) = 0) :
    E.SplitSingular :=
  ‚ü®‚ü®‚Äπ_‚Ä∫‚ü©, Œ±, by convert cond; exact Subsingleton.elim _ _‚ü©

variable {E} in
/-- The variable change of a `SplitSingular` weierstrass curve is `SplitSingular`. -/
def SplitSingular.variableChange [IsReduced R] [E.SplitSingular] (f : VariableChange R) :
    (f ‚Ä¢ E).SplitSingular where
  nonempty_singular := inferInstance
  Œ± := f.u‚Åª¬π * (E.Œ±‚ÇÅ - f.s)
  cond' := by
    rw [‚Üê singularityX]
    letI := (inferInstanceAs (Nonempty E.Singular)).some
    letI := (inferInstanceAs (Nonempty (f ‚Ä¢ E).Singular)).some
    simp only [variableChange_a‚ÇÅ, variableChange_a‚ÇÇ, singularityX_variableChange,
      VariableChange.inv_def]
    linear_combination f.u‚Åª¬π ^ 2 * SplitSingular.cond E

variable {E} in
/-- The other `SplitSingular` instance obtained by swapping the two tangents -/
abbrev SplitSingular.swap [IsReduced R] [E.SplitSingular] : E.SplitSingular where
  Œ± := E.Œ±‚ÇÇ
  cond' := by delta Œ±‚ÇÅ Œ±‚ÇÇ; linear_combination SplitSingular.cond' E

variable {E} in
lemma SplitSingular.swap_swap [IsReduced R] (H : E.SplitSingular) :
    H.swap.swap = H := by
  cases H; dsimp [swap, Œ±‚ÇÇ, Œ±‚ÇÅ]; congr; simp

lemma SplitSingular.ext [IsDomain R] (h‚ÇÅ h‚ÇÇ : E.SplitSingular) :
    h‚ÇÅ = h‚ÇÇ ‚à® h‚ÇÅ = h‚ÇÇ.swap := by
  have : (h‚ÇÅ.Œ± - h‚ÇÇ.Œ±) * (h‚ÇÅ.Œ± + h‚ÇÇ.Œ± + E.a‚ÇÅ) = 0 := by
    linear_combination h‚ÇÅ.cond' - h‚ÇÇ.cond'
  refine (mul_eq_zero.mp this).imp ?_ ?_
  all_goals { cases h‚ÇÅ; cases h‚ÇÇ; intro h; congr; dsimp only [Œ±‚ÇÅ, Œ±‚ÇÇ]; linear_combination h }

end WeierstrassCurve
