/-
Copyright (c) 2024 David Kurniadi Angdinata. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Kurniadi Angdinata
-/
import Mathlib.AlgebraicGeometry.EllipticCurve.Affine.Point
import Mathlib.NumberTheory.EllipticDivisibilitySequence

/-!
# Division polynomials of Weierstrass curves

This file defines certain polynomials associated to division polynomials of Weierstrass curves.
These are defined in terms of the auxiliary sequences for normalised elliptic divisibility sequences
(EDS) as defined in `Mathlib/NumberTheory/EllipticDivisibilitySequence.lean`.

## Mathematical background

Let `W` be a Weierstrass curve over a commutative ring `R`. The sequence of `n`-division polynomials
`Ïˆâ‚™ âˆˆ R[X, Y]` of `W` is the normalised EDS with initial values
* `Ïˆâ‚€ := 0`,
* `Ïˆâ‚ := 1`,
* `Ïˆâ‚‚ := 2Y + aâ‚X + aâ‚ƒ`,
* `Ïˆâ‚ƒ := 3Xâ´ + bâ‚‚XÂ³ + 3bâ‚„XÂ² + 3bâ‚†X + bâ‚ˆ`, and
* `Ïˆâ‚„ := Ïˆâ‚‚ â¬ (2Xâ¶ + bâ‚‚Xâµ + 5bâ‚„Xâ´ + 10bâ‚†XÂ³ + 10bâ‚ˆXÂ² + (bâ‚‚bâ‚ˆ - bâ‚„bâ‚†)X + (bâ‚„bâ‚ˆ - bâ‚†Â²))`.

Furthermore, define the associated sequences `Ï†â‚™, Ï‰â‚™ âˆˆ R[X, Y]` by
* `Ï†â‚™ := XÏˆâ‚™Â² - Ïˆâ‚™â‚Šâ‚ â¬ Ïˆâ‚™â‚‹â‚`, and
* `Ï‰â‚™ := (Ïˆâ‚‚â‚™ / Ïˆâ‚™ - Ïˆâ‚™ â¬ (aâ‚Ï†â‚™ + aâ‚ƒÏˆâ‚™Â²)) / 2`.

Note that `Ï‰â‚™` is always well-defined as a polynomial in `R[X, Y]`. As a start, it can be shown by
induction that `Ïˆâ‚™` always divides `Ïˆâ‚‚â‚™` in `R[X, Y]`, so that `Ïˆâ‚‚â‚™ / Ïˆâ‚™` is always well-defined as
a polynomial, while division by `2` is well-defined when `R` has characteristic different from `2`.
In general, it can be shown that `2` always divides the polynomial `Ïˆâ‚‚â‚™ / Ïˆâ‚™ - Ïˆâ‚™ â¬ (aâ‚Ï†â‚™ + aâ‚ƒÏˆâ‚™Â²)`
in the characteristic `0` universal ring `ð“¡[X, Y] := â„¤[Aâ‚, Aâ‚‚, Aâ‚ƒ, Aâ‚„, Aâ‚†][X, Y]` of `W`, where the
`Aáµ¢` are indeterminates. Then `Ï‰â‚™` can be equivalently defined as the image of this division under
the associated universal morphism `ð“¡[X, Y] â†’ R[X, Y]` mapping `Aáµ¢` to `aáµ¢`.

Now, in the coordinate ring `R[W]`, note that `Ïˆâ‚‚Â²` is congruent to the polynomial
`Î¨â‚‚Sq := 4XÂ³ + bâ‚‚XÂ² + 2bâ‚„X + bâ‚† âˆˆ R[X]`. As such, the recurrences of a normalised EDS show that
`Ïˆâ‚™ / Ïˆâ‚‚` are congruent to certain polynomials in `R[W]`. In particular, define `preÎ¨â‚™ âˆˆ R[X]` as
the auxiliary sequence for a normalised EDS with extra parameter `Î¨â‚‚SqÂ²` and initial values
* `preÎ¨â‚€ := 0`,
* `preÎ¨â‚ := 1`,
* `preÎ¨â‚‚ := 1`,
* `preÎ¨â‚ƒ := Ïˆâ‚ƒ`, and
* `preÎ¨â‚„ := Ïˆâ‚„ / Ïˆâ‚‚`.

The corresponding normalised EDS `Î¨â‚™ âˆˆ R[X, Y]` is then given by
* `Î¨â‚™ := preÎ¨â‚™ â¬ Ïˆâ‚‚` if `n` is even, and
* `Î¨â‚™ := preÎ¨â‚™` if `n` is odd.

Furthermore, define the associated sequences `Î¨Sqâ‚™, Î¦â‚™ âˆˆ R[X]` by
* `Î¨Sqâ‚™ := preÎ¨â‚™Â² â¬ Î¨â‚‚Sq` if `n` is even,
* `Î¨Sqâ‚™ := preÎ¨â‚™Â²` if `n` is odd,
* `Î¦â‚™ := XÎ¨Sqâ‚™ - preÎ¨â‚™â‚Šâ‚ â¬ preÎ¨â‚™â‚‹â‚` if `n` is even, and
* `Î¦â‚™ := XÎ¨Sqâ‚™ - preÎ¨â‚™â‚Šâ‚ â¬ preÎ¨â‚™â‚‹â‚ â¬ Î¨â‚‚Sq` if `n` is odd.

With these definitions, `Ïˆâ‚™ âˆˆ R[X, Y]` and `Ï†â‚™ âˆˆ R[X, Y]` are congruent in `R[W]` to `Î¨â‚™ âˆˆ R[X, Y]`
and `Î¦â‚™ âˆˆ R[X]` respectively, which are defined in terms of `Î¨â‚‚Sq âˆˆ R[X]` and `preÎ¨â‚™ âˆˆ R[X]`.

## Main definitions

* `WeierstrassCurve.preÎ¨`: the univariate polynomials `preÎ¨â‚™`.
* `WeierstrassCurve.Î¨Sq`: the univariate polynomials `Î¨Sqâ‚™`.
* `WeierstrassCurve.Î¨`: the bivariate polynomials `Î¨â‚™`.
* `WeierstrassCurve.Î¦`: the univariate polynomials `Î¦â‚™`.
* `WeierstrassCurve.Ïˆ`: the bivariate `n`-division polynomials `Ïˆâ‚™`.
* `WeierstrassCurve.Ï†`: the bivariate polynomials `Ï†â‚™`.
* TODO: the bivariate polynomials `Ï‰â‚™`.

## Implementation notes

Analogously to `Mathlib/NumberTheory/EllipticDivisibilitySequence.lean`, the bivariate polynomials
`Î¨â‚™` are defined in terms of the univariate polynomials `preÎ¨â‚™`. This is done partially to avoid
ring division, but more crucially to allow the definition of `Î¨Sqâ‚™` and `Î¦â‚™` as univariate
polynomials without needing to work under the coordinate ring, and to allow the computation of their
leading terms without ambiguity. Furthermore, evaluating these polynomials at a rational point on
`W` recovers their original definition up to linear combinations of the Weierstrass equation of `W`,
hence also avoiding the need to work in the coordinate ring.

TODO: implementation notes for the definition of `Ï‰â‚™`.

## References

[J Silverman, *The Arithmetic of Elliptic Curves*][silverman2009]

## Tags

elliptic curve, division polynomial, torsion point
-/

open Polynomial
open scoped Polynomial.Bivariate

local macro "C_simp" : tactic =>
  `(tactic| simp only [map_ofNat, C_0, C_1, C_neg, C_add, C_sub, C_mul, C_pow])

universe r s u v

namespace WeierstrassCurve

variable {R : Type r} {S : Type s} [CommRing R] [CommRing S] (W : WeierstrassCurve R)

section Î¨â‚‚Sq

/-! ### The univariate polynomial `Î¨â‚‚Sq` -/

/-- The `2`-division polynomial `Ïˆâ‚‚ = Î¨â‚‚`. -/
noncomputable def Ïˆâ‚‚ : R[X][Y] :=
  W.toAffine.polynomialY

/-- The univariate polynomial `Î¨â‚‚Sq` congruent to `Ïˆâ‚‚Â²`. -/
noncomputable def Î¨â‚‚Sq : R[X] :=
  C 4 * X ^ 3 + C W.bâ‚‚ * X ^ 2 + C (2 * W.bâ‚„) * X + C W.bâ‚†

lemma C_Î¨â‚‚Sq : C W.Î¨â‚‚Sq = W.Ïˆâ‚‚ ^ 2 - 4 * W.toAffine.polynomial := by
  rw [Î¨â‚‚Sq, Ïˆâ‚‚, bâ‚‚, bâ‚„, bâ‚†, Affine.polynomialY, Affine.polynomial]
  C_simp
  ring1

lemma Ïˆâ‚‚_sq : W.Ïˆâ‚‚ ^ 2 = C W.Î¨â‚‚Sq + 4 * W.toAffine.polynomial := by
  simp [C_Î¨â‚‚Sq]

lemma Affine.CoordinateRing.mk_Ïˆâ‚‚_sq : mk W W.Ïˆâ‚‚ ^ 2 = mk W (C W.Î¨â‚‚Sq) := by
  simp [C_Î¨â‚‚Sq]

-- TODO: remove `twoTorsionPolynomial` in favour of `Î¨â‚‚Sq`
lemma Î¨â‚‚Sq_eq : W.Î¨â‚‚Sq = W.twoTorsionPolynomial.toPoly :=
  rfl

end Î¨â‚‚Sq

section preÎ¨'

/-! ### The univariate polynomials `preÎ¨â‚™` for `n âˆˆ â„•` -/

/-- The `3`-division polynomial `Ïˆâ‚ƒ = Î¨â‚ƒ`. -/
noncomputable def Î¨â‚ƒ : R[X] :=
  3 * X ^ 4 + C W.bâ‚‚ * X ^ 3 + 3 * C W.bâ‚„ * X ^ 2 + 3 * C W.bâ‚† * X + C W.bâ‚ˆ

/-- The univariate polynomial `preÎ¨â‚„`, which is auxiliary to the 4-division polynomial
`Ïˆâ‚„ = Î¨â‚„ = preÎ¨â‚„Ïˆâ‚‚`. -/
noncomputable def preÎ¨â‚„ : R[X] :=
  2 * X ^ 6 + C W.bâ‚‚ * X ^ 5 + 5 * C W.bâ‚„ * X ^ 4 + 10 * C W.bâ‚† * X ^ 3 + 10 * C W.bâ‚ˆ * X ^ 2 +
    C (W.bâ‚‚ * W.bâ‚ˆ - W.bâ‚„ * W.bâ‚†) * X + C (W.bâ‚„ * W.bâ‚ˆ - W.bâ‚† ^ 2)

/-- The univariate polynomials `preÎ¨â‚™` for `n âˆˆ â„•`, which are auxiliary to the bivariate polynomials
`Î¨â‚™` congruent to the bivariate `n`-division polynomials `Ïˆâ‚™`. -/
noncomputable def preÎ¨' (n : â„•) : R[X] :=
  preNormEDS' (W.Î¨â‚‚Sq ^ 2) W.Î¨â‚ƒ W.preÎ¨â‚„ n

@[simp]
lemma preÎ¨'_zero : W.preÎ¨' 0 = 0 :=
  preNormEDS'_zero ..

@[simp]
lemma preÎ¨'_one : W.preÎ¨' 1 = 1 :=
  preNormEDS'_one ..

@[simp]
lemma preÎ¨'_two : W.preÎ¨' 2 = 1 :=
  preNormEDS'_two ..

@[simp]
lemma preÎ¨'_three : W.preÎ¨' 3 = W.Î¨â‚ƒ :=
  preNormEDS'_three ..

@[simp]
lemma preÎ¨'_four : W.preÎ¨' 4 = W.preÎ¨â‚„ :=
  preNormEDS'_four ..

lemma preÎ¨'_even (m : â„•) : W.preÎ¨' (2 * (m + 3)) =
    W.preÎ¨' (m + 2) ^ 2 * W.preÎ¨' (m + 3) * W.preÎ¨' (m + 5) -
      W.preÎ¨' (m + 1) * W.preÎ¨' (m + 3) * W.preÎ¨' (m + 4) ^ 2 :=
  preNormEDS'_even ..

lemma preÎ¨'_odd (m : â„•) : W.preÎ¨' (2 * (m + 2) + 1) =
    W.preÎ¨' (m + 4) * W.preÎ¨' (m + 2) ^ 3 * (if Even m then W.Î¨â‚‚Sq ^ 2 else 1) -
      W.preÎ¨' (m + 1) * W.preÎ¨' (m + 3) ^ 3 * (if Even m then 1 else W.Î¨â‚‚Sq ^ 2) :=
  preNormEDS'_odd ..

end preÎ¨'

section preÎ¨

/-! ### The univariate polynomials `preÎ¨â‚™` for `n âˆˆ â„¤` -/

/-- The univariate polynomials `preÎ¨â‚™` for `n âˆˆ â„¤`, which are auxiliary to the bivariate polynomials
`Î¨â‚™` congruent to the bivariate `n`-division polynomials `Ïˆâ‚™`. -/
noncomputable def preÎ¨ (n : â„¤) : R[X] :=
  preNormEDS (W.Î¨â‚‚Sq ^ 2) W.Î¨â‚ƒ W.preÎ¨â‚„ n

@[simp]
lemma preÎ¨_ofNat (n : â„•) : W.preÎ¨ n = W.preÎ¨' n :=
  preNormEDS_ofNat ..

@[simp]
lemma preÎ¨_zero : W.preÎ¨ 0 = 0 :=
  preNormEDS_zero ..

@[simp]
lemma preÎ¨_one : W.preÎ¨ 1 = 1 :=
  preNormEDS_one ..

@[simp]
lemma preÎ¨_two : W.preÎ¨ 2 = 1 :=
  preNormEDS_two ..

@[simp]
lemma preÎ¨_three : W.preÎ¨ 3 = W.Î¨â‚ƒ :=
  preNormEDS_three ..

@[simp]
lemma preÎ¨_four : W.preÎ¨ 4 = W.preÎ¨â‚„ :=
  preNormEDS_four ..

@[simp]
lemma preÎ¨_neg (n : â„¤) : W.preÎ¨ (-n) = -W.preÎ¨ n :=
  preNormEDS_neg ..

lemma preÎ¨_even (m : â„¤) : W.preÎ¨ (2 * m) =
    W.preÎ¨ (m - 1) ^ 2 * W.preÎ¨ m * W.preÎ¨ (m + 2) -
      W.preÎ¨ (m - 2) * W.preÎ¨ m * W.preÎ¨ (m + 1) ^ 2 :=
  preNormEDS_even ..

@[deprecated (since := "2025-05-15")] alias preÎ¨_even_ofNat := preÎ¨_even

lemma preÎ¨_odd (m : â„¤) : W.preÎ¨ (2 * m + 1) =
    W.preÎ¨ (m + 2) * W.preÎ¨ m ^ 3 * (if Even m then W.Î¨â‚‚Sq ^ 2 else 1) -
      W.preÎ¨ (m - 1) * W.preÎ¨ (m + 1) ^ 3 * (if Even m then 1 else W.Î¨â‚‚Sq ^ 2) :=
  preNormEDS_odd ..

@[deprecated (since := "2025-05-15")] alias preÎ¨_odd_ofNat := preÎ¨_odd

end preÎ¨

section Î¨Sq

/-! ### The univariate polynomials `Î¨Sqâ‚™` -/

/-- The univariate polynomials `Î¨Sqâ‚™` congruent to `Ïˆâ‚™Â²`. -/
noncomputable def Î¨Sq (n : â„¤) : R[X] :=
  W.preÎ¨ n ^ 2 * if Even n then W.Î¨â‚‚Sq else 1

@[simp]
lemma Î¨Sq_ofNat (n : â„•) : W.Î¨Sq n = W.preÎ¨' n ^ 2 * if Even n then W.Î¨â‚‚Sq else 1 := by
  simp [Î¨Sq]

@[simp]
lemma Î¨Sq_zero : W.Î¨Sq 0 = 0 := by
  simp [Î¨Sq]

@[simp]
lemma Î¨Sq_one : W.Î¨Sq 1 = 1 := by
  simp [Î¨Sq]

@[simp]
lemma Î¨Sq_two : W.Î¨Sq 2 = W.Î¨â‚‚Sq := by
  simp [Î¨Sq]

@[simp]
lemma Î¨Sq_three : W.Î¨Sq 3 = W.Î¨â‚ƒ ^ 2 := by
  simp [Î¨Sq, show Â¬Even (3 : â„¤) by decide]

@[simp]
lemma Î¨Sq_four : W.Î¨Sq 4 = W.preÎ¨â‚„ ^ 2 * W.Î¨â‚‚Sq := by
  simp [Î¨Sq, show Â¬Odd (4 : â„¤) by decide]

@[simp]
lemma Î¨Sq_neg (n : â„¤) : W.Î¨Sq (-n) = W.Î¨Sq n := by
  simp [Î¨Sq]

lemma Î¨Sq_even (m : â„¤) : W.Î¨Sq (2 * m) =
    (W.preÎ¨ (m - 1) ^ 2 * W.preÎ¨ m * W.preÎ¨ (m + 2) -
      W.preÎ¨ (m - 2) * W.preÎ¨ m * W.preÎ¨ (m + 1) ^ 2) ^ 2 * W.Î¨â‚‚Sq := by
  rw [Î¨Sq, preÎ¨_even, if_pos <| even_two_mul m]

@[deprecated (since := "2025-05-15")] alias Î¨Sq_even_ofNat := Î¨Sq_even

lemma Î¨Sq_odd (m : â„¤) : W.Î¨Sq (2 * m + 1) =
    (W.preÎ¨ (m + 2) * W.preÎ¨ m ^ 3 * (if Even m then W.Î¨â‚‚Sq ^ 2 else 1) -
      W.preÎ¨ (m - 1) * W.preÎ¨ (m + 1) ^ 3 * (if Even m then 1 else W.Î¨â‚‚Sq ^ 2)) ^ 2 := by
  rw [Î¨Sq, preÎ¨_odd, if_neg m.not_even_two_mul_add_one, mul_one]

@[deprecated (since := "2025-05-15")] alias Î¨Sq_odd_ofNat := Î¨Sq_odd

end Î¨Sq

section Î¨

/-! ### The bivariate polynomials `Î¨â‚™` -/

/-- The bivariate polynomials `Î¨â‚™` congruent to the `n`-division polynomials `Ïˆâ‚™`. -/
protected noncomputable def Î¨ (n : â„¤) : R[X][Y] :=
  C (W.preÎ¨ n) * if Even n then W.Ïˆâ‚‚ else 1

open WeierstrassCurve (Î¨)

@[simp]
lemma Î¨_ofNat (n : â„•) : W.Î¨ n = C (W.preÎ¨' n) * if Even n then W.Ïˆâ‚‚ else 1 := by
  simp [Î¨]

@[simp]
lemma Î¨_zero : W.Î¨ 0 = 0 := by
  simp [Î¨]

@[simp]
lemma Î¨_one : W.Î¨ 1 = 1 := by
  simp [Î¨]

@[simp]
lemma Î¨_two : W.Î¨ 2 = W.Ïˆâ‚‚ := by
  simp [Î¨]

@[simp]
lemma Î¨_three : W.Î¨ 3 = C W.Î¨â‚ƒ := by
  simp [Î¨, show Â¬Even (3 : â„¤) by decide]

@[simp]
lemma Î¨_four : W.Î¨ 4 = C W.preÎ¨â‚„ * W.Ïˆâ‚‚ := by
  simp [Î¨, show Â¬Odd (4 : â„¤) by decide]

@[simp]
lemma Î¨_neg (n : â„¤) : W.Î¨ (-n) = -W.Î¨ n := by
  simp_rw [Î¨, preÎ¨_neg, C_neg, neg_mul, even_neg]

lemma Î¨_even (m : â„¤) : W.Î¨ (2 * m) * W.Ïˆâ‚‚ =
    W.Î¨ (m - 1) ^ 2 * W.Î¨ m * W.Î¨ (m + 2) - W.Î¨ (m - 2) * W.Î¨ m * W.Î¨ (m + 1) ^ 2 := by
  simp_rw [Î¨, preÎ¨_even, if_pos <| even_two_mul m, Int.even_add, Int.even_sub, even_two, iff_true,
    Int.not_even_one, iff_false]
  split_ifs <;> C_simp <;> ring1

@[deprecated (since := "2025-05-15")] alias Î¨_even_ofNat := Î¨_even

lemma Î¨_odd (m : â„¤) : W.Î¨ (2 * m + 1) =
    W.Î¨ (m + 2) * W.Î¨ m ^ 3 - W.Î¨ (m - 1) * W.Î¨ (m + 1) ^ 3 +
      W.toAffine.polynomial * (16 * W.toAffine.polynomial - 8 * W.Ïˆâ‚‚ ^ 2) *
        C (if Even m then W.preÎ¨ (m + 2) * W.preÎ¨ m ^ 3
            else -W.preÎ¨ (m - 1) * W.preÎ¨ (m + 1) ^ 3) := by
  simp_rw [Î¨, preÎ¨_odd, if_neg m.not_even_two_mul_add_one, Int.even_add, Int.even_sub, even_two,
    iff_true, Int.not_even_one, iff_false]
  split_ifs <;> C_simp <;> rw [C_Î¨â‚‚Sq] <;> ring1

@[deprecated (since := "2025-05-15")] alias Î¨_odd_ofNat := Î¨_odd

lemma Affine.CoordinateRing.mk_Î¨_sq (n : â„¤) : mk W (W.Î¨ n) ^ 2 = mk W (C <| W.Î¨Sq n) := by
  simp_rw [Î¨, Î¨Sq, map_mul, apply_ite C, apply_ite <| mk W, mul_pow, ite_pow, mk_Ïˆâ‚‚_sq, map_one,
    one_pow, map_pow]

end Î¨

section Î¦

/-! ### The univariate polynomials `Î¦â‚™` -/

/-- The univariate polynomials `Î¦â‚™` congruent to `Ï†â‚™`. -/
protected noncomputable def Î¦ (n : â„¤) : R[X] :=
  X * W.Î¨Sq n - W.preÎ¨ (n + 1) * W.preÎ¨ (n - 1) * if Even n then 1 else W.Î¨â‚‚Sq

open WeierstrassCurve (Î¦)

@[simp]
lemma Î¦_ofNat (n : â„•) : W.Î¦ (n + 1) =
    X * W.preÎ¨' (n + 1) ^ 2 * (if Even n then 1 else W.Î¨â‚‚Sq) -
      W.preÎ¨' (n + 2) * W.preÎ¨' n * (if Even n then W.Î¨â‚‚Sq else 1) := by
  rw [Î¦, add_sub_cancel_right]
  norm_cast
  simp_rw [Î¨Sq_ofNat, Nat.even_add_one, ite_not, â† mul_assoc, preÎ¨_ofNat]

@[simp]
lemma Î¦_zero : W.Î¦ 0 = 1 := by
  simp [Î¦]

@[simp]
lemma Î¦_one : W.Î¦ 1 = X := by
  simp [Î¦]

@[simp]
lemma Î¦_two : W.Î¦ 2 = X ^ 4 - C W.bâ‚„ * X ^ 2 - C (2 * W.bâ‚†) * X - C W.bâ‚ˆ := by
  rw [show 2 = ((1 : â„•) + 1 : â„¤) by rfl, Î¦_ofNat, preÎ¨'_two, if_neg Nat.not_even_one, Î¨â‚‚Sq,
    preÎ¨'_three, preÎ¨'_one, if_neg Nat.not_even_one, Î¨â‚ƒ]
  C_simp
  ring1

@[simp]
lemma Î¦_three : W.Î¦ 3 = X * W.Î¨â‚ƒ ^ 2 - W.preÎ¨â‚„ * W.Î¨â‚‚Sq := by
  rw [show 3 = ((2 : â„•) + 1 : â„¤) by rfl, Î¦_ofNat, preÎ¨'_three, if_pos <| by decide, mul_one,
    preÎ¨'_four, preÎ¨'_two, mul_one, if_pos even_two]

@[simp]
lemma Î¦_four : W.Î¦ 4 = X * W.preÎ¨â‚„ ^ 2 * W.Î¨â‚‚Sq - W.Î¨â‚ƒ * (W.preÎ¨â‚„ * W.Î¨â‚‚Sq ^ 2 - W.Î¨â‚ƒ ^ 3) := by
  rw [show 4 = ((3 : â„•) + 1 : â„¤) by rfl, Î¦_ofNat, preÎ¨'_four, if_neg <| by decide,
    show 3 + 2 = 2 * 2 + 1 by rfl, preÎ¨'_odd, preÎ¨'_four, preÎ¨'_two, if_pos Even.zero, preÎ¨'_one,
    preÎ¨'_three, if_pos Even.zero, if_neg <| by decide]
  ring1

@[simp]
lemma Î¦_neg (n : â„¤) : W.Î¦ (-n) = W.Î¦ n := by
  simp_rw [Î¦, Î¨Sq_neg, â† sub_neg_eq_add, â† neg_sub', sub_neg_eq_add, â† neg_add', preÎ¨_neg,
    neg_mul_neg, mul_comm <| W.preÎ¨ <| n - 1, even_neg]

end Î¦

section Ïˆ

/-! ### The bivariate polynomials `Ïˆâ‚™` -/

/-- The bivariate `n`-division polynomials `Ïˆâ‚™`. -/
protected noncomputable def Ïˆ (n : â„¤) : R[X][Y] :=
  normEDS W.Ïˆâ‚‚ (C W.Î¨â‚ƒ) (C W.preÎ¨â‚„) n

open WeierstrassCurve (Î¨ Ïˆ)

@[simp]
lemma Ïˆ_zero : W.Ïˆ 0 = 0 :=
  normEDS_zero ..

@[simp]
lemma Ïˆ_one : W.Ïˆ 1 = 1 :=
  normEDS_one ..

@[simp]
lemma Ïˆ_two : W.Ïˆ 2 = W.Ïˆâ‚‚ :=
  normEDS_two ..

@[simp]
lemma Ïˆ_three : W.Ïˆ 3 = C W.Î¨â‚ƒ :=
  normEDS_three ..

@[simp]
lemma Ïˆ_four : W.Ïˆ 4 = C W.preÎ¨â‚„ * W.Ïˆâ‚‚ :=
  normEDS_four ..

@[simp]
lemma Ïˆ_neg (n : â„¤) : W.Ïˆ (-n) = -W.Ïˆ n :=
  normEDS_neg ..

lemma Ïˆ_even (m : â„¤) : W.Ïˆ (2 * m) * W.Ïˆâ‚‚ =
    W.Ïˆ (m - 1) ^ 2 * W.Ïˆ m * W.Ïˆ (m + 2) - W.Ïˆ (m - 2) * W.Ïˆ m * W.Ïˆ (m + 1) ^ 2 :=
  normEDS_even ..

@[deprecated (since := "2025-05-15")] alias Ïˆ_even_ofNat := Ïˆ_even

lemma Ïˆ_odd (m : â„¤) : W.Ïˆ (2 * m + 1) =
    W.Ïˆ (m + 2) * W.Ïˆ m ^ 3 - W.Ïˆ (m - 1) * W.Ïˆ (m + 1) ^ 3 :=
  normEDS_odd ..

@[deprecated (since := "2025-05-15")] alias Ïˆ_odd_ofNat := Ïˆ_odd

lemma Affine.CoordinateRing.mk_Ïˆ (n : â„¤) : mk W (W.Ïˆ n) = mk W (W.Î¨ n) := by
  simp_rw [Ïˆ, normEDS, Î¨, preÎ¨, map_mul, map_preNormEDS, map_pow, â† mk_Ïˆâ‚‚_sq, â† pow_mul]

end Ïˆ

section Ï†

/-! ### The bivariate polynomials `Ï†â‚™` -/

/-- The bivariate polynomials `Ï†â‚™`. -/
protected noncomputable def Ï† (n : â„¤) : R[X][Y] :=
  C X * W.Ïˆ n ^ 2 - W.Ïˆ (n + 1) * W.Ïˆ (n - 1)

open WeierstrassCurve (Î¨ Î¦ Ï†)

@[simp]
lemma Ï†_zero : W.Ï† 0 = 1 := by
  simp [Ï†]

@[simp]
lemma Ï†_one : W.Ï† 1 = C X := by
  simp [Ï†]

@[simp]
lemma Ï†_two : W.Ï† 2 = C X * W.Ïˆâ‚‚ ^ 2 - C W.Î¨â‚ƒ := by
  simp [Ï†]

@[simp]
lemma Ï†_three : W.Ï† 3 = C X * C W.Î¨â‚ƒ ^ 2 - C W.preÎ¨â‚„ * W.Ïˆâ‚‚ ^ 2 := by
  simp [Ï†, mul_assoc, sq]

@[simp]
lemma Ï†_four :
    W.Ï† 4 = C X * C W.preÎ¨â‚„ ^ 2 * W.Ïˆâ‚‚ ^ 2 - C W.preÎ¨â‚„ * W.Ïˆâ‚‚ ^ 4 * C W.Î¨â‚ƒ + C W.Î¨â‚ƒ ^ 4 := by
  rw [Ï†, Ïˆ_four, show (4 + 1 : â„¤) = 2 * 2 + 1 by rfl, Ïˆ_odd, two_add_two_eq_four, Ïˆ_four,
    show (2 - 1 : â„¤) = 1 by rfl, Ïˆ_two, Ïˆ_one, two_add_one_eq_three, show (4 - 1 : â„¤) = 3 by rfl,
    Ïˆ_three]
  ring1

@[simp]
lemma Ï†_neg (n : â„¤) : W.Ï† (-n) = W.Ï† n := by
  simp_rw [Ï†, Ïˆ_neg, neg_sq, â† sub_neg_eq_add, â† neg_sub', sub_neg_eq_add, â† neg_add', Ïˆ_neg,
    neg_mul_neg, mul_comm <| W.Ïˆ <| n - 1]

lemma Affine.CoordinateRing.mk_Ï† (n : â„¤) : mk W (W.Ï† n) = mk W (C <| W.Î¦ n) := by
  simp_rw [Ï†, Î¦, map_sub, map_mul, map_pow, mk_Ïˆ, mk_Î¨_sq, Î¨, map_mul,
    mul_mul_mul_comm _ <| mk W <| ite .., Int.even_add_one, Int.even_sub_one, ite_not, â† sq,
    apply_ite C, apply_ite <| mk W, ite_pow, map_one, one_pow, mk_Ïˆâ‚‚_sq]

end Ï†

section Map

/-! ### Maps across ring homomorphisms -/

open WeierstrassCurve (Î¨ Î¦ Ïˆ Ï†)

variable (f : R â†’+* S)

@[simp]
lemma map_Ïˆâ‚‚ : (W.map f).Ïˆâ‚‚ = W.Ïˆâ‚‚.map (mapRingHom f) := by
  simp_rw [Ïˆâ‚‚, Affine.map_polynomialY]

@[simp]
lemma map_Î¨â‚‚Sq : (W.map f).Î¨â‚‚Sq = W.Î¨â‚‚Sq.map f := by
  simp [Î¨â‚‚Sq, map_ofNat]

@[simp]
lemma map_Î¨â‚ƒ : (W.map f).Î¨â‚ƒ = W.Î¨â‚ƒ.map f := by
  simp [Î¨â‚ƒ]

@[simp]
lemma map_preÎ¨â‚„ : (W.map f).preÎ¨â‚„ = W.preÎ¨â‚„.map f := by
  simp [preÎ¨â‚„]

@[simp]
lemma map_preÎ¨' (n : â„•) : (W.map f).preÎ¨' n = (W.preÎ¨' n).map f := by
  simp [preÎ¨', â† coe_mapRingHom]

@[simp]
lemma map_preÎ¨ (n : â„¤) : (W.map f).preÎ¨ n = (W.preÎ¨ n).map f := by
  simp [preÎ¨, â† coe_mapRingHom]

@[simp]
lemma map_Î¨Sq (n : â„¤) : (W.map f).Î¨Sq n = (W.Î¨Sq n).map f := by
  simp [Î¨Sq, â† coe_mapRingHom, apply_ite <| mapRingHom f]

@[simp]
lemma map_Î¨ (n : â„¤) : (W.map f).Î¨ n = (W.Î¨ n).map (mapRingHom f) := by
  rw [â† coe_mapRingHom]
  simp [Î¨, apply_ite <| mapRingHom _]

@[simp]
lemma map_Î¦ (n : â„¤) : (W.map f).Î¦ n = (W.Î¦ n).map f := by
  rw [â† coe_mapRingHom]
  simp [Î¦, map_sub, apply_ite <| mapRingHom f]

@[simp]
lemma map_Ïˆ (n : â„¤) : (W.map f).Ïˆ n = (W.Ïˆ n).map (mapRingHom f) := by
  rw [â† coe_mapRingHom]
  simp [Ïˆ]

@[simp]
lemma map_Ï† (n : â„¤) : (W.map f).Ï† n = (W.Ï† n).map (mapRingHom f) := by
  simp [Ï†]

end Map

section BaseChange

/-! ### Base changes across algebra homomorphisms -/

variable [Algebra R S] {A : Type u} [CommRing A] [Algebra R A] [Algebra S A] [IsScalarTower R S A]
  {B : Type v} [CommRing B] [Algebra R B] [Algebra S B] [IsScalarTower R S B] (f : A â†’â‚[S] B)

lemma baseChange_Ïˆâ‚‚ : (W.baseChange B).Ïˆâ‚‚ = (W.baseChange A).Ïˆâ‚‚.map (mapRingHom f) := by
  rw [â† map_Ïˆâ‚‚, map_baseChange]

lemma baseChange_Î¨â‚‚Sq : (W.baseChange B).Î¨â‚‚Sq = (W.baseChange A).Î¨â‚‚Sq.map f := by
  rw [â† map_Î¨â‚‚Sq, map_baseChange]

lemma baseChange_Î¨â‚ƒ : (W.baseChange B).Î¨â‚ƒ = (W.baseChange A).Î¨â‚ƒ.map f := by
  rw [â† map_Î¨â‚ƒ, map_baseChange]

lemma baseChange_preÎ¨â‚„ : (W.baseChange B).preÎ¨â‚„ = (W.baseChange A).preÎ¨â‚„.map f := by
  rw [â† map_preÎ¨â‚„, map_baseChange]

lemma baseChange_preÎ¨' (n : â„•) : (W.baseChange B).preÎ¨' n = ((W.baseChange A).preÎ¨' n).map f := by
  rw [â† map_preÎ¨', map_baseChange]

lemma baseChange_preÎ¨ (n : â„¤) : (W.baseChange B).preÎ¨ n = ((W.baseChange A).preÎ¨ n).map f := by
  rw [â† map_preÎ¨, map_baseChange]

lemma baseChange_Î¨Sq (n : â„¤) : (W.baseChange B).Î¨Sq n = ((W.baseChange A).Î¨Sq n).map f := by
  rw [â† map_Î¨Sq, map_baseChange]

lemma baseChange_Î¨ (n : â„¤) : (W.baseChange B).Î¨ n = ((W.baseChange A).Î¨ n).map (mapRingHom f) := by
  rw [â† map_Î¨, map_baseChange]

lemma baseChange_Î¦ (n : â„¤) : (W.baseChange B).Î¦ n = ((W.baseChange A).Î¦ n).map f := by
  rw [â† map_Î¦, map_baseChange]

lemma baseChange_Ïˆ (n : â„¤) : (W.baseChange B).Ïˆ n = ((W.baseChange A).Ïˆ n).map (mapRingHom f) := by
  rw [â† map_Ïˆ, map_baseChange]

lemma baseChange_Ï† (n : â„¤) : (W.baseChange B).Ï† n = ((W.baseChange A).Ï† n).map (mapRingHom f) := by
  rw [â† map_Ï†, map_baseChange]

end BaseChange

end WeierstrassCurve
