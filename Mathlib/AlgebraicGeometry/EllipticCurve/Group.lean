/-
Copyright (c) 2023 David Kurniadi Angdinata. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Kurniadi Angdinata
-/
import Mathlib.AlgebraicGeometry.EllipticCurve.Jacobian
import Mathlib.AlgebraicGeometry.EllipticCurve.Projective
import Mathlib.LinearAlgebra.FreeModule.Norm
import Mathlib.RingTheory.ClassGroup
import Mathlib.RingTheory.Polynomial.UniqueFactorization

/-!
# Group law on Weierstrass curves

This file proves that the nonsingular rational points on a Weierstrass curve form an abelian group
under the geometric group law defined in `Mathlib/AlgebraicGeometry/EllipticCurve/Affine.lean`, in
`Mathlib/AlgebraicGeometry/EllipticCurve/Jacobian.lean`, and in
`Mathlib/AlgebraicGeometry/EllipticCurve/Projective.lean`.

## Mathematical background

Let `W` be a Weierstrass curve over a field `F` given by a Weierstrass equation `W(X, Y) = 0` in
affine coordinates. As in `Mathlib/AlgebraicGeometry/EllipticCurve/Affine.lean`, the set of
nonsingular rational points `W‚üÆF‚üØ` of `W` consist of the unique point at infinity `ùìû` and
nonsingular affine points `(x, y)`. With this description, there is an addition-preserving injection
between `W‚üÆF‚üØ` and the ideal class group of the coordinate ring `F[W] := F[X, Y] / ‚ü®W(X, Y)‚ü©` of
`W`. This is defined by mapping the point at infinity `ùìû` to the trivial ideal class and an affine
point `(x, y)` to the ideal class of the invertible fractional ideal `‚ü®X - x, Y - y‚ü©`. Proving that
this is well-defined and preserves addition reduce to checking several equalities of integral
ideals, which is done in `WeierstrassCurve.Affine.CoordinateRing.XYIdeal_neg_mul` and in
`WeierstrassCurve.Affine.CoordinateRing.XYIdeal_mul_XYIdeal` via explicit ideal computations. Now
`F[W]` is a free rank two `F[X]`-algebra with basis `{1, Y}`, so every element of `F[W]` is of the
form `p + qY` for some `p, q ‚àà F[X]`, and there is an algebra norm `N : F[W] ‚Üí F[X]`. Injectivity
can then be shown by computing the degree of such a norm `N(p + qY)` in two different ways, which is
done in `WeierstrassCurve.Affine.CoordinateRing.degree_norm_smul_basis` and in the auxiliary lemmas
in the proof of `WeierstrassCurve.Affine.Point.instAddCommGroup`.

When `W` is given in Jacobian coordinates, `WeierstrassCurve.Jacobian.Point.toAffineAddEquiv` pulls
back the group law on `WeierstrassCurve.Affine.Point` to `WeierstrassCurve.Jacobian.Point`.

When `W` is given in projective coordinates, `WeierstrassCurve.Projective.Point.toAffineAddEquiv`
pulls back the group law on `WeierstrassCurve.Affine.Point` to `WeierstrassCurve.Projective.Point`.

## Main definitions

 * `WeierstrassCurve.Affine.CoordinateRing`: the coordinate ring `F[W]` of a Weierstrass curve `W`.
 * `WeierstrassCurve.Affine.CoordinateRing.basis`: the power basis of `F[W]` over `F[X]`.

## Main statements

 * `WeierstrassCurve.Affine.CoordinateRing.instIsDomainCoordinateRing`: the coordinate ring of an
    affine Weierstrass curve is an integral domain.
 * `WeierstrassCurve.Affine.CoordinateRing.degree_norm_smul_basis`: the degree of the norm of an
    element in the coordinate ring of an affine Weierstrass curve in terms of the power basis.
 * `WeierstrassCurve.Affine.Point.instAddCommGroup`: the type of nonsingular rational points on
    an affine Weierstrass curve forms an abelian group under addition.
 * `WeierstrassCurve.Jacobian.Point.instAddCommGroup`: the type of nonsingular rational points on a
    Jacobian Weierstrass curve forms an abelian group under addition.
 * `WeierstrassCurve.Projective.Point.instAddCommGroup`: the type of nonsingular rational points on
    a projective Weierstrass curve forms an abelian group under addition.

## References

https://drops.dagstuhl.de/storage/00lipics/lipics-vol268-itp2023/LIPIcs.ITP.2023.6/LIPIcs.ITP.2023.6.pdf

## Tags

elliptic curve, group law, class group
-/

open Ideal Polynomial

open scoped nonZeroDivisors Polynomial.Bivariate

local macro "C_simp" : tactic =>
  `(tactic| simp only [map_ofNat, C_0, C_1, C_neg, C_add, C_sub, C_mul, C_pow])

local macro "eval_simp" : tactic =>
  `(tactic| simp only [eval_C, eval_X, eval_neg, eval_add, eval_sub, eval_mul, eval_pow])

universe u v

namespace WeierstrassCurve.Affine

/-! ## Weierstrass curves in affine coordinates -/

variable {R : Type u} {S : Type v} [CommRing R] [CommRing S] (W : Affine R) (f : R ‚Üí+* S)

-- Porting note: in Lean 3, this is a `def` under a `derive comm_ring` tag.
-- This generates a reducible instance of `comm_ring` for `coordinate_ring`. In certain
-- circumstances this might be extremely slow, because all instances in its definition are unified
-- exponentially many times. In this case, one solution is to manually add the local attribute
-- `local attribute [irreducible] coordinate_ring.comm_ring` to block this type-level unification.
-- In Lean 4, this is no longer an issue and is now an `abbrev`. See Zulip thread:
-- https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/.E2.9C.94.20class_group.2Emk
/-- The coordinate ring $R[W] := R[X, Y] / \langle W(X, Y) \rangle$ of `W`. -/
abbrev CoordinateRing : Type u :=
  AdjoinRoot W.polynomial

/-- The function field $R(W) := \mathrm{Frac}(R[W])$ of `W`. -/
abbrev FunctionField : Type u :=
  FractionRing W.CoordinateRing

namespace CoordinateRing

section Algebra

/-! ### The coordinate ring as an `R[X]`-algebra -/

noncomputable instance : Algebra R W.CoordinateRing :=
  Quotient.algebra R

noncomputable instance : Algebra R[X] W.CoordinateRing :=
  Quotient.algebra R[X]

instance : IsScalarTower R R[X] W.CoordinateRing :=
  Quotient.isScalarTower R R[X] _

instance [Subsingleton R] : Subsingleton W.CoordinateRing :=
  Module.subsingleton R[X] _

-- Porting note: added the abbreviation `mk` for `AdjoinRoot.mk W.polynomial`
/-- The natural ring homomorphism mapping an element of `R[X][Y]` to an element of `R[W]`. -/
noncomputable abbrev mk : R[X][Y] ‚Üí+* W.CoordinateRing :=
  AdjoinRoot.mk W.polynomial

-- Porting note: added `classical` explicitly
/-- The basis $\{1, Y\}$ for the coordinate ring $R[W]$ over the polynomial ring $R[X]$. -/
protected noncomputable def basis : Basis (Fin 2) R[X] W.CoordinateRing := by
  classical exact (subsingleton_or_nontrivial R).by_cases (fun _ => default) fun _ =>
    (AdjoinRoot.powerBasis' W.monic_polynomial).basis.reindex <| finCongr W.natDegree_polynomial

lemma basis_apply (n : Fin 2) :
    CoordinateRing.basis W n = (AdjoinRoot.powerBasis' W.monic_polynomial).gen ^ (n : ‚Ñï) := by
  classical
  nontriviality R
  rw [CoordinateRing.basis, Or.by_cases, dif_neg <| not_subsingleton R, Basis.reindex_apply,
    PowerBasis.basis_eq_pow]
  rfl

-- Porting note: added `@[simp]` in lieu of `coe_basis`
@[simp]
lemma basis_zero : CoordinateRing.basis W 0 = 1 := by
  simpa only [basis_apply] using pow_zero _

-- Porting note: added `@[simp]` in lieu of `coe_basis`
@[simp]
lemma basis_one : CoordinateRing.basis W 1 = mk W Y := by
  simpa only [basis_apply] using pow_one _

-- Porting note: removed `@[simp]` in lieu of `basis_zero` and `basis_one`
lemma coe_basis : (CoordinateRing.basis W : Fin 2 ‚Üí W.CoordinateRing) = ![1, mk W Y] := by
  ext n
  fin_cases n
  exacts [basis_zero W, basis_one W]

variable {W} in
lemma smul (x : R[X]) (y : W.CoordinateRing) : x ‚Ä¢ y = mk W (C x) * y :=
  (algebraMap_smul W.CoordinateRing x y).symm

variable {W} in
lemma smul_basis_eq_zero {p q : R[X]} (hpq : p ‚Ä¢ (1 : W.CoordinateRing) + q ‚Ä¢ mk W Y = 0) :
    p = 0 ‚àß q = 0 := by
  have h := Fintype.linearIndependent_iff.mp (CoordinateRing.basis W).linearIndependent ![p, q]
  rw [Fin.sum_univ_succ, basis_zero, Fin.sum_univ_one, Fin.succ_zero_eq_one, basis_one] at h
  exact ‚ü®h hpq 0, h hpq 1‚ü©

variable {W} in
lemma exists_smul_basis_eq (x : W.CoordinateRing) :
    ‚àÉ p q : R[X], p ‚Ä¢ (1 : W.CoordinateRing) + q ‚Ä¢ mk W Y = x := by
  have h := (CoordinateRing.basis W).sum_equivFun x
  rw [Fin.sum_univ_succ, Fin.sum_univ_one, basis_zero, Fin.succ_zero_eq_one, basis_one] at h
  exact ‚ü®_, _, h‚ü©

lemma smul_basis_mul_C (y : R[X]) (p q : R[X]) :
    (p ‚Ä¢ (1 : W.CoordinateRing) + q ‚Ä¢ mk W Y) * mk W (C y) =
      (p * y) ‚Ä¢ (1 : W.CoordinateRing) + (q * y) ‚Ä¢ mk W Y := by
  simp only [smul, _root_.map_mul]
  ring1

lemma smul_basis_mul_Y (p q : R[X]) : (p ‚Ä¢ (1 : W.CoordinateRing) + q ‚Ä¢ mk W Y) * mk W Y =
    (q * (X ^ 3 + C W.a‚ÇÇ * X ^ 2 + C W.a‚ÇÑ * X + C W.a‚ÇÜ)) ‚Ä¢ (1 : W.CoordinateRing) +
      (p - q * (C W.a‚ÇÅ * X + C W.a‚ÇÉ)) ‚Ä¢ mk W Y := by
  have Y_sq : mk W Y ^ 2 =
      mk W (C (X ^ 3 + C W.a‚ÇÇ * X ^ 2 + C W.a‚ÇÑ * X + C W.a‚ÇÜ) - C (C W.a‚ÇÅ * X + C W.a‚ÇÉ) * Y) := by
    exact AdjoinRoot.mk_eq_mk.mpr ‚ü®1, by rw [polynomial]; ring1‚ü©
  simp only [smul, add_mul, mul_assoc, ‚Üê sq, Y_sq, C_sub, map_sub, C_mul, _root_.map_mul]
  ring1

/-- The ring homomorphism `R[W] ‚Üí+* S[W.map f]` induced by a ring homomorphism `f : R ‚Üí+* S`. -/
noncomputable def map : W.CoordinateRing ‚Üí+* (W.map f).toAffine.CoordinateRing :=
  AdjoinRoot.lift ((AdjoinRoot.of _).comp <| mapRingHom f)
    ((AdjoinRoot.root (WeierstrassCurve.map W f).toAffine.polynomial)) <| by
      rw [‚Üê eval‚ÇÇ_map, ‚Üê map_polynomial, AdjoinRoot.eval‚ÇÇ_root]

lemma map_mk (x : R[X][Y]) : map W f (mk W x) = mk (W.map f) (x.map <| mapRingHom f) := by
  rw [map, AdjoinRoot.lift_mk, ‚Üê eval‚ÇÇ_map]
  exact AdjoinRoot.aeval_eq <| x.map <| mapRingHom f

variable {W} in
protected lemma map_smul (x : R[X]) (y : W.CoordinateRing) :
    map W f (x ‚Ä¢ y) = x.map f ‚Ä¢ map W f y := by
  rw [smul, _root_.map_mul, map_mk, map_C, smul]
  rfl

variable {f} in
lemma map_injective (hf : Function.Injective f) : Function.Injective <| map W f :=
  (injective_iff_map_eq_zero _).mpr fun y hy => by
    obtain ‚ü®p, q, rfl‚ü© := exists_smul_basis_eq y
    simp_rw [map_add, CoordinateRing.map_smul, map_one, map_mk, map_X] at hy
    obtain ‚ü®hp, hq‚ü© := smul_basis_eq_zero hy
    rw [Polynomial.map_eq_zero_iff hf] at hp hq
    simp_rw [hp, hq, zero_smul, add_zero]

instance [IsDomain R] : IsDomain W.CoordinateRing :=
  have : IsDomain (W.map <| algebraMap R <| FractionRing R).toAffine.CoordinateRing :=
    AdjoinRoot.isDomain_of_prime (irreducible_polynomial _).prime
  (map_injective W <| IsFractionRing.injective R <| FractionRing R).isDomain

end Algebra

section Ring

/-! ### Ideals in the coordinate ring over a ring -/

/-- The class of the element $X - x$ in $R[W]$ for some $x \in R$. -/
noncomputable def XClass (x : R) : W.CoordinateRing :=
  mk W <| C <| X - C x

lemma XClass_ne_zero [Nontrivial R] (x : R) : XClass W x ‚â† 0 :=
  AdjoinRoot.mk_ne_zero_of_natDegree_lt W.monic_polynomial (C_ne_zero.mpr <| X_sub_C_ne_zero x) <|
    by rw [natDegree_polynomial, natDegree_C]; norm_num1

/-- The class of the element $Y - y(X)$ in $R[W]$ for some $y(X) \in R[X]$. -/
noncomputable def YClass (y : R[X]) : W.CoordinateRing :=
  mk W <| Y - C y

lemma YClass_ne_zero [Nontrivial R] (y : R[X]) : YClass W y ‚â† 0 :=
  AdjoinRoot.mk_ne_zero_of_natDegree_lt W.monic_polynomial (X_sub_C_ne_zero y) <|
    by rw [natDegree_polynomial, natDegree_X_sub_C]; norm_num1

lemma C_addPolynomial (x y L : R) : mk W (C <| W.addPolynomial x y L) =
    mk W ((Y - C (linePolynomial x y L)) * (W.negPolynomial - C (linePolynomial x y L))) :=
  AdjoinRoot.mk_eq_mk.mpr ‚ü®1, by rw [W.C_addPolynomial, add_sub_cancel_left, mul_one]‚ü©

/-- The ideal $\langle X - x \rangle$ of $R[W]$ for some $x \in R$. -/
noncomputable def XIdeal (x : R) : Ideal W.CoordinateRing :=
  span {XClass W x}

/-- The ideal $\langle Y - y(X) \rangle$ of $R[W]$ for some $y(X) \in R[X]$. -/
noncomputable def YIdeal (y : R[X]) : Ideal W.CoordinateRing :=
  span {YClass W y}

/-- The ideal $\langle X - x, Y - y(X) \rangle$ of $R[W]$ for some $x \in R$ and $y(X) \in R[X]$. -/
noncomputable def XYIdeal (x : R) (y : R[X]) : Ideal W.CoordinateRing :=
  span {XClass W x, YClass W y}

lemma XYIdeal_eq‚ÇÅ (x y L : R) : XYIdeal W x (C y) = XYIdeal W x (linePolynomial x y L) := by
  simp only [XYIdeal, XClass, YClass, linePolynomial]
  rw [‚Üê span_pair_add_mul_right <| mk W <| C <| C <| -L, ‚Üê _root_.map_mul, ‚Üê map_add]
  apply congr_arg (_ ‚àò _ ‚àò _ ‚àò _)
  C_simp
  ring1

lemma XYIdeal_add_eq (x‚ÇÅ x‚ÇÇ y‚ÇÅ L : R) : XYIdeal W (W.addX x‚ÇÅ x‚ÇÇ L) (C <| W.addY x‚ÇÅ x‚ÇÇ y‚ÇÅ L) =
    span {mk W <| W.negPolynomial - C (linePolynomial x‚ÇÅ y‚ÇÅ L)} ‚äî XIdeal W (W.addX x‚ÇÅ x‚ÇÇ L) := by
  simp only [XYIdeal, XIdeal, XClass, YClass, addY, negAddY, negY, negPolynomial, linePolynomial]
  rw [sub_sub <| -(Y : R[X][Y]), neg_sub_left (Y : R[X][Y]), map_neg, span_singleton_neg, sup_comm,
    ‚Üê span_insert, ‚Üê span_pair_add_mul_right <| mk W <| C <| C <| W.a‚ÇÅ + L, ‚Üê _root_.map_mul,
    ‚Üê map_add]
  apply congr_arg (_ ‚àò _ ‚àò _ ‚àò _)
  C_simp
  ring1

/-- The $R$-algebra isomorphism from $R[W] / \langle X - x, Y - y(X) \rangle$ to $R$ obtained by
evaluation at $y(X)$ and at $x$ provided that $W(x, y(x)) = 0$. -/
noncomputable def quotientXYIdealEquiv {x : R} {y : R[X]} (h : (W.polynomial.eval y).eval x = 0) :
    (W.CoordinateRing ‚ß∏ XYIdeal W x y) ‚âÉ‚Çê[R] R :=
  ((quotientEquivAlgOfEq R <| by
      simp only [XYIdeal, XClass, YClass, ‚Üê Set.image_pair, ‚Üê map_span]; rfl).trans <|
        DoubleQuot.quotQuotEquivQuotOfLE‚Çê R <| (span_singleton_le_iff_mem _).mpr <|
          mem_span_C_X_sub_C_X_sub_C_iff_eval_eval_eq_zero.mpr h).trans
    quotientSpanCXSubCXSubCAlgEquiv

end Ring

section Field

/-! ### Ideals in the coordinate ring over a field -/

variable {F : Type u} [Field F] {W : Affine F}

lemma C_addPolynomial_slope {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} (h‚ÇÅ : W.Equation x‚ÇÅ y‚ÇÅ) (h‚ÇÇ : W.Equation x‚ÇÇ y‚ÇÇ)
    (hxy : x‚ÇÅ = x‚ÇÇ ‚Üí y‚ÇÅ ‚â† W.negY x‚ÇÇ y‚ÇÇ) : mk W (C <| W.addPolynomial x‚ÇÅ y‚ÇÅ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) =
      -(XClass W x‚ÇÅ * XClass W x‚ÇÇ * XClass W (W.addX x‚ÇÅ x‚ÇÇ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ)) := by
  simp only [addPolynomial_slope h‚ÇÅ h‚ÇÇ hxy, C_neg, mk, map_neg, neg_inj, _root_.map_mul]
  rfl

lemma XYIdeal_eq‚ÇÇ {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} (h‚ÇÅ : W.Equation x‚ÇÅ y‚ÇÅ)
    (h‚ÇÇ : W.Equation x‚ÇÇ y‚ÇÇ) (hxy : x‚ÇÅ = x‚ÇÇ ‚Üí y‚ÇÅ ‚â† W.negY x‚ÇÇ y‚ÇÇ) :
    XYIdeal W x‚ÇÇ (C y‚ÇÇ) = XYIdeal W x‚ÇÇ (linePolynomial x‚ÇÅ y‚ÇÅ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) := by
  have hy‚ÇÇ : y‚ÇÇ = (linePolynomial x‚ÇÅ y‚ÇÅ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ).eval x‚ÇÇ := by
    by_cases hx : x‚ÇÅ = x‚ÇÇ
    ¬∑ rcases hx, Y_eq_of_Y_ne h‚ÇÅ h‚ÇÇ hx <| hxy hx with ‚ü®rfl, rfl‚ü©
      field_simp [linePolynomial, sub_ne_zero_of_ne <| hxy rfl]
    ¬∑ field_simp [linePolynomial, slope_of_X_ne hx, sub_ne_zero_of_ne hx]
      ring1
  nth_rw 1 [hy‚ÇÇ]
  simp only [XYIdeal, XClass, YClass, linePolynomial]
  rw [‚Üê span_pair_add_mul_right <| mk W <| C <| C <| -W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ, ‚Üê _root_.map_mul,
    ‚Üê map_add]
  apply congr_arg (_ ‚àò _ ‚àò _ ‚àò _)
  eval_simp
  C_simp
  ring1

lemma XYIdeal_neg_mul {x y : F} (h : W.Nonsingular x y) :
    XYIdeal W x (C <| W.negY x y) * XYIdeal W x (C y) = XIdeal W x := by
  have Y_rw : (Y - C (C y)) * (Y - C (C <| W.negY x y)) -
      C (X - C x) * (C (X ^ 2 + C (x + W.a‚ÇÇ) * X + C (x ^ 2 + W.a‚ÇÇ * x + W.a‚ÇÑ)) - C (C W.a‚ÇÅ) * Y) =
        W.polynomial * 1 := by
    linear_combination (norm := (rw [negY, polynomial]; C_simp; ring1))
      congr_arg C (congr_arg C ((equation_iff ..).mp h.left).symm)
  simp_rw [XYIdeal, XClass, YClass, span_pair_mul_span_pair, mul_comm, ‚Üê _root_.map_mul,
    AdjoinRoot.mk_eq_mk.mpr ‚ü®1, Y_rw‚ü©, _root_.map_mul, span_insert,
    ‚Üê span_singleton_mul_span_singleton, ‚Üê Ideal.mul_sup, ‚Üê span_insert]
  convert mul_top (_ : Ideal W.CoordinateRing) using 2
  simp_rw [‚Üê Set.image_singleton (f := mk W), ‚Üê Set.image_insert_eq, ‚Üê map_span]
  convert map_top (R := F[X][Y]) (mk W) using 1
  apply congr_arg
  simp_rw [eq_top_iff_one, mem_span_insert', mem_span_singleton']
  rcases ((nonsingular_iff' ..).mp h).right with hx | hy
  ¬∑ let W_X := W.a‚ÇÅ * y - (3 * x ^ 2 + 2 * W.a‚ÇÇ * x + W.a‚ÇÑ)
    refine
      ‚ü®C <| C W_X‚Åª¬π * -(X + C (2 * x + W.a‚ÇÇ)), C <| C <| W_X‚Åª¬π * W.a‚ÇÅ, 0, C <| C <| W_X‚Åª¬π * -1, ?_‚ü©
    rw [‚Üê mul_right_inj' <| C_ne_zero.mpr <| C_ne_zero.mpr hx]
    simp only [W_X, mul_add, ‚Üê mul_assoc, ‚Üê C_mul, mul_inv_cancel‚ÇÄ hx]
    C_simp
    ring1
  ¬∑ let W_Y := 2 * y + W.a‚ÇÅ * x + W.a‚ÇÉ
    refine ‚ü®0, C <| C W_Y‚Åª¬π, C <| C <| W_Y‚Åª¬π * -1, 0, ?_‚ü©
    rw [negY, ‚Üê mul_right_inj' <| C_ne_zero.mpr <| C_ne_zero.mpr hy]
    simp only [W_Y, mul_add, ‚Üê mul_assoc, ‚Üê C_mul, mul_inv_cancel‚ÇÄ hy]
    C_simp
    ring1

private lemma XYIdeal'_mul_inv {x y : F} (h : W.Nonsingular x y) :
    XYIdeal W x (C y) * (XYIdeal W x (C <| W.negY x y) *
        (XIdeal W x : FractionalIdeal W.CoordinateRing‚Å∞ W.FunctionField)‚Åª¬π) = 1 := by
  rw [‚Üê mul_assoc, ‚Üê FractionalIdeal.coeIdeal_mul, mul_comm <| XYIdeal W .., XYIdeal_neg_mul h,
    XIdeal, FractionalIdeal.coe_ideal_span_singleton_mul_inv W.FunctionField <| XClass_ne_zero W x]

lemma XYIdeal_mul_XYIdeal {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} (h‚ÇÅ : W.Equation x‚ÇÅ y‚ÇÅ) (h‚ÇÇ : W.Equation x‚ÇÇ y‚ÇÇ)
    (hxy : x‚ÇÅ = x‚ÇÇ ‚Üí y‚ÇÅ ‚â† W.negY x‚ÇÇ y‚ÇÇ) :
    XIdeal W (W.addX x‚ÇÅ x‚ÇÇ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) * (XYIdeal W x‚ÇÅ (C y‚ÇÅ) * XYIdeal W x‚ÇÇ (C y‚ÇÇ)) =
      YIdeal W (linePolynomial x‚ÇÅ y‚ÇÅ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) *
        XYIdeal W (W.addX x‚ÇÅ x‚ÇÇ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ)
          (C <| W.addY x‚ÇÅ x‚ÇÇ y‚ÇÅ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) := by
  have sup_rw : ‚àÄ a b c d : Ideal W.CoordinateRing, a ‚äî (b ‚äî (c ‚äî d)) = a ‚äî d ‚äî b ‚äî c :=
    fun _ _ c _ => by rw [‚Üê sup_assoc, sup_comm c, sup_sup_sup_comm, ‚Üê sup_assoc]
  rw [XYIdeal_add_eq, XIdeal, mul_comm, XYIdeal_eq‚ÇÅ W x‚ÇÅ y‚ÇÅ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ, XYIdeal,
    XYIdeal_eq‚ÇÇ h‚ÇÅ h‚ÇÇ hxy, XYIdeal, span_pair_mul_span_pair]
  simp_rw [span_insert, sup_rw, Ideal.sup_mul, span_singleton_mul_span_singleton]
  rw [‚Üê neg_eq_iff_eq_neg.mpr <| C_addPolynomial_slope h‚ÇÅ h‚ÇÇ hxy, span_singleton_neg,
    C_addPolynomial, _root_.map_mul, YClass]
  simp_rw [mul_comm <| XClass W x‚ÇÅ, mul_assoc, ‚Üê span_singleton_mul_span_singleton, ‚Üê Ideal.mul_sup]
  rw [span_singleton_mul_span_singleton, ‚Üê span_insert,
    ‚Üê span_pair_add_mul_right <| -(XClass W <| W.addX x‚ÇÅ x‚ÇÇ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ), mul_neg,
    ‚Üê sub_eq_add_neg, ‚Üê sub_mul, ‚Üê map_sub <| mk W, sub_sub_sub_cancel_right, span_insert,
    ‚Üê span_singleton_mul_span_singleton, ‚Üê sup_rw, ‚Üê Ideal.sup_mul, ‚Üê Ideal.sup_mul]
  apply congr_arg (_ ‚àò _)
  convert top_mul (_ : Ideal W.CoordinateRing)
  simp_rw [XClass, ‚Üê Set.image_singleton (f := mk W), ‚Üê map_span, ‚Üê Ideal.map_sup, eq_top_iff_one,
    mem_map_iff_of_surjective _ AdjoinRoot.mk_surjective, ‚Üê span_insert, mem_span_insert',
    mem_span_singleton']
  by_cases hx : x‚ÇÅ = x‚ÇÇ
  ¬∑ rcases hx, Y_eq_of_Y_ne h‚ÇÅ h‚ÇÇ hx (hxy hx) with ‚ü®rfl, rfl‚ü©
    let y := (y‚ÇÅ - W.negY x‚ÇÅ y‚ÇÅ) ^ 2
    replace hxy := pow_ne_zero 2 <| sub_ne_zero_of_ne <| hxy rfl
    refine ‚ü®1 + C (C <| y‚Åª¬π * 4) * W.polynomial,
      ‚ü®C <| C y‚Åª¬π * (C 4 * X ^ 2 + C (4 * x‚ÇÅ + W.b‚ÇÇ) * X + C (4 * x‚ÇÅ ^ 2 + W.b‚ÇÇ * x‚ÇÅ + 2 * W.b‚ÇÑ)),
        0, C (C y‚Åª¬π) * (Y - W.negPolynomial), ?_‚ü©, by
      rw [map_add, map_one, _root_.map_mul <| mk W, AdjoinRoot.mk_self, mul_zero, add_zero]‚ü©
    rw [polynomial, negPolynomial, ‚Üê mul_right_inj' <| C_ne_zero.mpr <| C_ne_zero.mpr hxy]
    simp only [y, mul_add, ‚Üê mul_assoc, ‚Üê C_mul, mul_inv_cancel‚ÇÄ hxy]
    linear_combination (norm := (rw [b‚ÇÇ, b‚ÇÑ, negY]; C_simp; ring1))
      -4 * congr_arg C (congr_arg C <| (equation_iff ..).mp h‚ÇÅ)
  ¬∑ replace hx := sub_ne_zero_of_ne hx
    refine ‚ü®_, ‚ü®‚ü®C <| C (x‚ÇÅ - x‚ÇÇ)‚Åª¬π, C <| C <| (x‚ÇÅ - x‚ÇÇ)‚Åª¬π * -1, 0, ?_‚ü©, map_one _‚ü©‚ü©
    rw [‚Üê mul_right_inj' <| C_ne_zero.mpr <| C_ne_zero.mpr hx]
    simp only [‚Üê mul_assoc, mul_add, ‚Üê C_mul, mul_inv_cancel‚ÇÄ hx]
    C_simp
    ring1

/-- The non-zero fractional ideal $\langle X - x, Y - y \rangle$ of $F(W)$ for some $x, y \in F$. -/
noncomputable def XYIdeal' {x y : F} (h : W.Nonsingular x y) :
    (FractionalIdeal W.CoordinateRing‚Å∞ W.FunctionField)À£ :=
  Units.mkOfMulEqOne _ _ <| XYIdeal'_mul_inv h

lemma XYIdeal'_eq {x y : F} (h : W.Nonsingular x y) :
    (XYIdeal' h : FractionalIdeal W.CoordinateRing‚Å∞ W.FunctionField) = XYIdeal W x (C y) :=
  rfl

lemma mk_XYIdeal'_mul_mk_XYIdeal'_of_Yeq {x y : F} (h : W.Nonsingular x y) :
    ClassGroup.mk (XYIdeal' <| nonsingular_neg h) * ClassGroup.mk (XYIdeal' h) = 1 := by
  rw [‚Üê _root_.map_mul]
  exact
    (ClassGroup.mk_eq_one_of_coe_ideal <| by exact (FractionalIdeal.coeIdeal_mul ..).symm.trans <|
      FractionalIdeal.coeIdeal_inj.mpr <| XYIdeal_neg_mul h).mpr ‚ü®_, XClass_ne_zero W _, rfl‚ü©

lemma mk_XYIdeal'_mul_mk_XYIdeal' {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} (h‚ÇÅ : W.Nonsingular x‚ÇÅ y‚ÇÅ)
    (h‚ÇÇ : W.Nonsingular x‚ÇÇ y‚ÇÇ) (hxy : x‚ÇÅ = x‚ÇÇ ‚Üí y‚ÇÅ ‚â† W.negY x‚ÇÇ y‚ÇÇ) :
    ClassGroup.mk (XYIdeal' h‚ÇÅ) * ClassGroup.mk (XYIdeal' h‚ÇÇ) =
      ClassGroup.mk (XYIdeal' <| nonsingular_add h‚ÇÅ h‚ÇÇ hxy) := by
  rw [‚Üê _root_.map_mul]
  exact (ClassGroup.mk_eq_mk_of_coe_ideal (by exact (FractionalIdeal.coeIdeal_mul ..).symm) <|
      XYIdeal'_eq _).mpr
    ‚ü®_, _, XClass_ne_zero W _, YClass_ne_zero W _, XYIdeal_mul_XYIdeal h‚ÇÅ.left h‚ÇÇ.left hxy‚ü©

end Field

section Norm

/-! ### Norms on the coordinate ring -/

lemma norm_smul_basis (p q : R[X]) :
    Algebra.norm R[X] (p ‚Ä¢ (1 : W.CoordinateRing) + q ‚Ä¢ mk W Y) =
      p ^ 2 - p * q * (C W.a‚ÇÅ * X + C W.a‚ÇÉ) -
        q ^ 2 * (X ^ 3 + C W.a‚ÇÇ * X ^ 2 + C W.a‚ÇÑ * X + C W.a‚ÇÜ) := by
  simp_rw [Algebra.norm_eq_matrix_det <| CoordinateRing.basis W, Matrix.det_fin_two,
    Algebra.leftMulMatrix_eq_repr_mul, basis_zero, mul_one, basis_one, smul_basis_mul_Y, map_add,
    Finsupp.add_apply, map_smul, Finsupp.smul_apply, ‚Üê basis_zero, ‚Üê basis_one,
    Basis.repr_self_apply, if_pos, one_ne_zero, if_false, smul_eq_mul]
  ring1

lemma coe_norm_smul_basis (p q : R[X]) :
    Algebra.norm R[X] (p ‚Ä¢ (1 : W.CoordinateRing) + q ‚Ä¢ mk W Y) =
      mk W ((C p + C q * X) * (C p + C q * (-(Y : R[X][Y]) - C (C W.a‚ÇÅ * X + C W.a‚ÇÉ)))) :=
  AdjoinRoot.mk_eq_mk.mpr
    ‚ü®C q ^ 2, by simp only [norm_smul_basis, polynomial]; C_simp; ring1‚ü©

lemma degree_norm_smul_basis [IsDomain R] (p q : R[X]) :
    (Algebra.norm R[X] <| p ‚Ä¢ (1 : W.CoordinateRing) + q ‚Ä¢ mk W Y).degree =
      max (2 ‚Ä¢ p.degree) (2 ‚Ä¢ q.degree + 3) := by
  have hdp : (p ^ 2).degree = 2 ‚Ä¢ p.degree := degree_pow p 2
  have hdpq : (p * q * (C W.a‚ÇÅ * X + C W.a‚ÇÉ)).degree ‚â§ p.degree + q.degree + 1 := by
    simpa only [degree_mul] using add_le_add_left degree_linear_le (p.degree + q.degree)
  have hdq :
      (q ^ 2 * (X ^ 3 + C W.a‚ÇÇ * X ^ 2 + C W.a‚ÇÑ * X + C W.a‚ÇÜ)).degree = 2 ‚Ä¢ q.degree + 3 := by
    rw [degree_mul, degree_pow, ‚Üê one_mul <| X ^ 3, ‚Üê C_1, degree_cubic <| one_ne_zero' R]
  rw [norm_smul_basis]
  by_cases hp : p = 0
  ¬∑ simpa only [hp, hdq, neg_zero, zero_sub, zero_mul, zero_pow two_ne_zero, degree_neg] using
      (max_bot_left _).symm
  ¬∑ by_cases hq : q = 0
    ¬∑ simpa only [hq, hdp, sub_zero, zero_mul, mul_zero, zero_pow two_ne_zero] using
        (max_bot_right _).symm
    ¬∑ rw [‚Üê not_congr degree_eq_bot] at hp hq
      -- Porting note: BUG `cases` tactic does not modify assumptions in `hp'` and `hq'`
      rcases hp' : p.degree with _ | dp -- `hp' : ` should be redundant
      ¬∑ exact (hp hp').elim -- `hp'` should be `rfl`
      ¬∑ rw [hp'] at hdp hdpq -- line should be redundant
        rcases hq' : q.degree with _ | dq -- `hq' : ` should be redundant
        ¬∑ exact (hq hq').elim -- `hq'` should be `rfl`
        ¬∑ rw [hq'] at hdpq hdq -- line should be redundant
          rcases le_or_lt dp (dq + 1) with hpq | hpq
          ¬∑ convert (degree_sub_eq_right_of_degree_lt <| (degree_sub_le _ _).trans_lt <|
                      max_lt_iff.mpr ‚ü®hdp.trans_lt _, hdpq.trans_lt _‚ü©).trans
              (max_eq_right_of_lt _).symm <;> rw [hdq] <;>
                exact WithBot.coe_lt_coe.mpr <| by dsimp; linarith only [hpq]
          ¬∑ rw [sub_sub]
            convert (degree_sub_eq_left_of_degree_lt <| (degree_add_le _ _).trans_lt <|
                      max_lt_iff.mpr ‚ü®hdpq.trans_lt _, hdq.trans_lt _‚ü©).trans
              (max_eq_left_of_lt _).symm <;> rw [hdp] <;>
                exact WithBot.coe_lt_coe.mpr <| by dsimp; linarith only [hpq]

variable {W} in
lemma degree_norm_ne_one [IsDomain R] (x : W.CoordinateRing) :
    (Algebra.norm R[X] x).degree ‚â† 1 := by
  rcases exists_smul_basis_eq x with ‚ü®p, q, rfl‚ü©
  rw [degree_norm_smul_basis]
  rcases p.degree with (_ | _ | _ | _) <;> cases q.degree
  any_goals rintro (_ | _)
  -- Porting note: replaced `dec_trivial` with `by exact (cmp_eq_lt_iff ..).mp rfl`
  exact (lt_max_of_lt_right <| by exact (cmp_eq_lt_iff ..).mp rfl).ne'

variable {W} in
lemma natDegree_norm_ne_one [IsDomain R] (x : W.CoordinateRing) :
    (Algebra.norm R[X] x).natDegree ‚â† 1 :=
  degree_norm_ne_one x ‚àò (degree_eq_iff_natDegree_eq_of_pos zero_lt_one).mpr

end Norm

end CoordinateRing

namespace Point

/-! ### The axioms for nonsingular rational points on a Weierstrass curve -/

variable {F : Type u} [Field F] {W : Affine F}

/-- The set function mapping an affine point $(x, y)$ of `W` to the class of the non-zero fractional
ideal $\langle X - x, Y - y \rangle$ of $F(W)$ in the class group of $F[W]$. -/
@[simp]
noncomputable def toClassFun : W.Point ‚Üí Additive (ClassGroup W.CoordinateRing)
  | 0 => 0
  | some h => Additive.ofMul <| ClassGroup.mk <| CoordinateRing.XYIdeal' h

/-- The group homomorphism mapping an affine point $(x, y)$ of `W` to the class of the non-zero
fractional ideal $\langle X - x, Y - y \rangle$ of $F(W)$ in the class group of $F[W]$. -/
@[simps]
noncomputable def toClass : W.Point ‚Üí+ Additive (ClassGroup W.CoordinateRing) where
  toFun := toClassFun
  map_zero' := rfl
  map_add' := by
    rintro (_ | @‚ü®x‚ÇÅ, y‚ÇÅ, h‚ÇÅ‚ü©) (_ | @‚ü®x‚ÇÇ, y‚ÇÇ, h‚ÇÇ‚ü©)
    any_goals simp only [zero_def, toClassFun, zero_add, add_zero]
    obtain ‚ü®rfl, rfl‚ü© | h := em (x‚ÇÅ = x‚ÇÇ ‚àß y‚ÇÅ = W.negY x‚ÇÇ y‚ÇÇ)
    ¬∑ rw [add_of_Y_eq rfl rfl]
      exact (CoordinateRing.mk_XYIdeal'_mul_mk_XYIdeal'_of_Yeq h‚ÇÇ).symm
    ¬∑ have h hx hy := h ‚ü®hx, hy‚ü©
      rw [add_of_imp h]
      exact (CoordinateRing.mk_XYIdeal'_mul_mk_XYIdeal' h‚ÇÅ h‚ÇÇ h).symm

lemma toClass_zero : toClass (0 : W.Point) = 0 :=
  rfl

lemma toClass_some {x y : F} (h : W.Nonsingular x y) :
    toClass (some h) = ClassGroup.mk (CoordinateRing.XYIdeal' h) :=
  rfl

private lemma add_eq_zero (P Q : W.Point) : P + Q = 0 ‚Üî P = -Q := by
  rcases P, Q with ‚ü®_ | @‚ü®x‚ÇÅ, y‚ÇÅ, _‚ü©, _ | @‚ü®x‚ÇÇ, y‚ÇÇ, _‚ü©‚ü©
  any_goals rfl
  ¬∑ rw [zero_def, zero_add, ‚Üê neg_eq_iff_eq_neg, neg_zero, eq_comm]
  ¬∑ rw [neg_some, some.injEq]
    constructor
    ¬∑ contrapose!; intro h; rw [add_of_imp h]; exact some_ne_zero _
    ¬∑ exact fun ‚ü®hx, hy‚ü© ‚Ü¶ add_of_Y_eq hx hy

lemma toClass_eq_zero (P : W.Point) : toClass P = 0 ‚Üî P = 0 := by
  constructor
  ¬∑ intro hP
    rcases P with (_ | ‚ü®h, _‚ü©)
    ¬∑ rfl
    ¬∑ rcases (ClassGroup.mk_eq_one_of_coe_ideal <| by rfl).mp hP with ‚ü®p, h0, hp‚ü©
      apply (p.natDegree_norm_ne_one _).elim
      rw [‚Üê finrank_quotient_span_eq_natDegree_norm (CoordinateRing.basis W) h0,
        ‚Üê (quotientEquivAlgOfEq F hp).toLinearEquiv.finrank_eq,
        (CoordinateRing.quotientXYIdealEquiv W h).toLinearEquiv.finrank_eq,
        Module.finrank_self]
  ¬∑ exact congr_arg toClass

lemma toClass_injective : Function.Injective <| @toClass _ _ W := by
  rintro (_ | h) _ hP
  all_goals rw [‚Üê neg_inj, ‚Üê add_eq_zero, ‚Üê toClass_eq_zero, map_add, ‚Üê hP]
  ¬∑ exact zero_add 0
  ¬∑ exact CoordinateRing.mk_XYIdeal'_mul_mk_XYIdeal'_of_Yeq h

noncomputable instance : AddCommGroup W.Point where
  nsmul := nsmulRec
  zsmul := zsmulRec
  zero_add := zero_add
  add_zero := add_zero
  neg_add_cancel _ := by rw [add_eq_zero]
  add_comm _ _ := toClass_injective <| by simp only [map_add, add_comm]
  add_assoc _ _ _ := toClass_injective <| by simp only [map_add, add_assoc]

end Point

end WeierstrassCurve.Affine

namespace WeierstrassCurve.Projective.Point

/-! ## Weierstrass curves in projective coordinates -/

variable {F : Type u} [Field F] {W : Projective F}

noncomputable instance : AddCommGroup W.Point where
  nsmul := nsmulRec
  zsmul := zsmulRec
  zero_add _ := (toAffineAddEquiv W).injective <| by
    simp only [map_add, toAffineAddEquiv_apply, toAffineLift_zero, zero_add]
  add_zero _ := (toAffineAddEquiv W).injective <| by
    simp only [map_add, toAffineAddEquiv_apply, toAffineLift_zero, add_zero]
  neg_add_cancel P := (toAffineAddEquiv W).injective <| by
    simp only [map_add, toAffineAddEquiv_apply, toAffineLift_neg, neg_add_cancel, toAffineLift_zero]
  add_comm _ _ := (toAffineAddEquiv W).injective <| by simp only [map_add, add_comm]
  add_assoc _ _ _ := (toAffineAddEquiv W).injective <| by simp only [map_add, add_assoc]

end WeierstrassCurve.Projective.Point

namespace WeierstrassCurve.Jacobian.Point

/-! ## Weierstrass curves in Jacobian coordinates -/

variable {F : Type u} [Field F] {W : Jacobian F}

noncomputable instance : AddCommGroup W.Point where
  nsmul := nsmulRec
  zsmul := zsmulRec
  zero_add _ := (toAffineAddEquiv W).injective <| by
    simp only [map_add, toAffineAddEquiv_apply, toAffineLift_zero, zero_add]
  add_zero _ := (toAffineAddEquiv W).injective <| by
    simp only [map_add, toAffineAddEquiv_apply, toAffineLift_zero, add_zero]
  neg_add_cancel P := (toAffineAddEquiv W).injective <| by
    simp only [map_add, toAffineAddEquiv_apply, toAffineLift_neg, neg_add_cancel, toAffineLift_zero]
  add_comm _ _ := (toAffineAddEquiv W).injective <| by simp only [map_add, add_comm]
  add_assoc _ _ _ := (toAffineAddEquiv W).injective <| by simp only [map_add, add_assoc]

end WeierstrassCurve.Jacobian.Point

namespace WeierstrassCurve.Affine.Point

/-! ## Elliptic curves in affine coordinates -/

variable {R : Type*} [Nontrivial R] [CommRing R] (E : WeierstrassCurve R) [E.IsElliptic]

/-- An affine point on an elliptic curve `E` over `R`. -/
def mk {x y : R} (h : E.toAffine.Equation x y) : E.toAffine.Point :=
  WeierstrassCurve.Affine.Point.some <| nonsingular E h

end WeierstrassCurve.Affine.Point
