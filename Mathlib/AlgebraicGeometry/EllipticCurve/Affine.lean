/-
Copyright (c) 2023 David Kurniadi Angdinata. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Kurniadi Angdinata
-/
import Mathlib.Algebra.Polynomial.Bivariate
import Mathlib.AlgebraicGeometry.EllipticCurve.Weierstrass
import Mathlib.AlgebraicGeometry.EllipticCurve.VariableChange

/-!
# Affine coordinates for Weierstrass curves

This file defines the type of points on a Weierstrass curve as an inductive, consisting of the point
at infinity and affine points satisfying a Weierstrass equation with a nonsingular condition. This
file also defines the negation and addition operations of the group law for this type, and proves
that they respect the Weierstrass equation and the nonsingular condition. The fact that they form an
abelian group is proven in `Mathlib/AlgebraicGeometry/EllipticCurve/Group.lean`.

## Mathematical background

Let `W` be a Weierstrass curve over a field `F` with coefficients `a·µ¢`. An *affine point*
on `W` is a tuple `(x, y)` of elements in `R` satisfying the *Weierstrass equation* `W(X, Y) = 0` in
*affine coordinates*, where `W(X, Y) := Y¬≤ + a‚ÇÅXY + a‚ÇÉY - (X¬≥ + a‚ÇÇX¬≤ + a‚ÇÑX + a‚ÇÜ)`. It is
*nonsingular* if its partial derivatives `W_X(x, y)` and `W_Y(x, y)` do not vanish simultaneously.

The nonsingular affine points on `W` can be given negation and addition operations defined by a
secant-and-tangent process.
 * Given a nonsingular affine point `P`, its *negation* `-P` is defined to be the unique third
    nonsingular point of intersection between `W` and the vertical line through `P`.
    Explicitly, if `P` is `(x, y)`, then `-P` is `(x, -y - a‚ÇÅx - a‚ÇÉ)`.
 * Given two nonsingular affine points `P` and `Q`, their *addition* `P + Q` is defined to be the
    negation of the unique third nonsingular point of intersection between `W` and the line `L`
    through `P` and `Q`. Explicitly, let `P` be `(x‚ÇÅ, y‚ÇÅ)` and let `Q` be `(x‚ÇÇ, y‚ÇÇ)`.
      * If `x‚ÇÅ = x‚ÇÇ` and `y‚ÇÅ = -y‚ÇÇ - a‚ÇÅx‚ÇÇ - a‚ÇÉ`, then `L` is vertical.
      * If `x‚ÇÅ = x‚ÇÇ` and `y‚ÇÅ ‚â† -y‚ÇÇ - a‚ÇÅx‚ÇÇ - a‚ÇÉ`, then `L` is the tangent of `W` at `P = Q`, and has
        slope `‚Ñì := (3x‚ÇÅ¬≤ + 2a‚ÇÇx‚ÇÅ + a‚ÇÑ - a‚ÇÅy‚ÇÅ) / (2y‚ÇÅ + a‚ÇÅx‚ÇÅ + a‚ÇÉ)`.
      * Otherwise `x‚ÇÅ ‚â† x‚ÇÇ`, then `L` is the secant of `W` through `P` and `Q`, and has slope
        `‚Ñì := (y‚ÇÅ - y‚ÇÇ) / (x‚ÇÅ - x‚ÇÇ)`.

    In the last two cases, the `X`-coordinate of `P + Q` is then the unique third solution of the
    equation obtained by substituting the line `Y = ‚Ñì(X - x‚ÇÅ) + y‚ÇÅ` into the Weierstrass equation,
    and can be written down explicitly as `x := ‚Ñì¬≤ + a‚ÇÅ‚Ñì - a‚ÇÇ - x‚ÇÅ - x‚ÇÇ` by inspecting the
    coefficients of `X¬≤`. The `Y`-coordinate of `P + Q`, after applying the final negation that maps
    `Y` to `-Y - a‚ÇÅX - a‚ÇÉ`, is precisely `y := -(‚Ñì(x - x‚ÇÅ) + y‚ÇÅ) - a‚ÇÅx - a‚ÇÉ`.

The type of nonsingular points `W‚üÆF‚üØ` in affine coordinates is an inductive, consisting of the
unique point at infinity `ùìû` and nonsingular affine points `(x, y)`. Then `W‚üÆF‚üØ` can be endowed with
a group law, with `ùìû` as the identity nonsingular point, which is uniquely determined by these
formulae.

## Main definitions

 * `WeierstrassCurve.Affine.Equation`: the Weierstrass equation of an affine Weierstrass curve.
 * `WeierstrassCurve.Affine.Nonsingular`: the nonsingular condition on an affine Weierstrass curve.
 * `WeierstrassCurve.Affine.Point`: a nonsingular rational point on an affine Weierstrass curve.
 * `WeierstrassCurve.Affine.Point.neg`: the negation operation on an affine Weierstrass curve.
 * `WeierstrassCurve.Affine.Point.add`: the addition operation on an affine Weierstrass curve.

## Main statements

 * `WeierstrassCurve.Affine.equation_neg`: negation preserves the Weierstrass equation.
 * `WeierstrassCurve.Affine.equation_add`: addition preserves the Weierstrass equation.
 * `WeierstrassCurve.Affine.nonsingular_neg`: negation preserves the nonsingular condition.
 * `WeierstrassCurve.Affine.nonsingular_add`: addition preserves the nonsingular condition.
 * `WeierstrassCurve.Affine.nonsingular_of_Œî_ne_zero`: an affine Weierstrass curve is nonsingular at
    every point if its discriminant is non-zero.
 * `WeierstrassCurve.Affine.nonsingular`: an affine elliptic curve is nonsingular at every point.

## Notations

 * `W‚üÆK‚üØ`: the group of nonsingular rational points on `W` base changed to `K`.

## References

[J Silverman, *The Arithmetic of Elliptic Curves*][silverman2009]

## Tags

elliptic curve, rational point, affine coordinates
-/

open Polynomial

open scoped Polynomial.Bivariate

local macro "C_simp" : tactic =>
  `(tactic| simp only [map_ofNat, C_0, C_1, C_neg, C_add, C_sub, C_mul, C_pow])

local macro "derivative_simp" : tactic =>
  `(tactic| simp only [derivative_C, derivative_X, derivative_X_pow, derivative_neg, derivative_add,
    derivative_sub, derivative_mul, derivative_sq])

local macro "eval_simp" : tactic =>
  `(tactic| simp only [eval_C, eval_X, eval_neg, eval_add, eval_sub, eval_mul, eval_pow, evalEval])

local macro "map_simp" : tactic =>
  `(tactic| simp only [map_ofNat, map_neg, map_add, map_sub, map_mul, map_pow, map_div‚ÇÄ,
    Polynomial.map_ofNat, map_C, map_X, Polynomial.map_neg, Polynomial.map_add, Polynomial.map_sub,
    Polynomial.map_mul, Polynomial.map_pow, Polynomial.map_div, coe_mapRingHom,
    WeierstrassCurve.map])

universe r s u v w

/-! ## Weierstrass curves -/

namespace WeierstrassCurve

variable {R : Type r} {S : Type s} {A F : Type u} {B K : Type v} {L : Type w}

variable (R) in
/-- An abbreviation for a Weierstrass curve in affine coordinates. -/
abbrev Affine : Type r :=
  WeierstrassCurve R

/-- The conversion from a Weierstrass curve to affine coordinates. -/
abbrev toAffine (W : WeierstrassCurve R) : Affine R :=
  W

namespace Affine

variable [CommRing R] [CommRing S] [CommRing A] [CommRing B] [Field F] [Field K] [Field L]
  {W' : Affine R} {W : Affine F}

section Equation

/-! ### Weierstrass equations -/

variable (W') in
/-- The polynomial `W(X, Y) := Y¬≤ + a‚ÇÅXY + a‚ÇÉY - (X¬≥ + a‚ÇÇX¬≤ + a‚ÇÑX + a‚ÇÜ)` associated to a Weierstrass
curve `W` over a ring `R` in affine coordinates.

For ease of polynomial manipulation, this is represented as a term of type `R[X][X]`, where the
inner variable represents `X` and the outer variable represents `Y`. For clarity, the alternative
notations `Y` and `R[X][Y]` are provided in the `Polynomial.Bivariate` scope to represent the outer
variable and the bivariate polynomial ring `R[X][X]` respectively. -/
noncomputable def polynomial : R[X][Y] :=
  Y ^ 2 + C (C W'.a‚ÇÅ * X + C W'.a‚ÇÉ) * Y - C (X ^ 3 + C W'.a‚ÇÇ * X ^ 2 + C W'.a‚ÇÑ * X + C W'.a‚ÇÜ)

lemma polynomial_eq : W'.polynomial = Cubic.toPoly
    ‚ü®0, 1, Cubic.toPoly ‚ü®0, 0, W'.a‚ÇÅ, W'.a‚ÇÉ‚ü©, Cubic.toPoly ‚ü®-1, -W'.a‚ÇÇ, -W'.a‚ÇÑ, -W'.a‚ÇÜ‚ü©‚ü© := by
  simp only [polynomial, Cubic.toPoly]
  C_simp
  ring1

lemma polynomial_ne_zero [Nontrivial R] : W'.polynomial ‚â† 0 := by
  rw [polynomial_eq]
  exact Cubic.ne_zero_of_b_ne_zero one_ne_zero

@[simp]
lemma degree_polynomial [Nontrivial R] : W'.polynomial.degree = 2 := by
  rw [polynomial_eq]
  exact Cubic.degree_of_b_ne_zero' one_ne_zero

@[simp]
lemma natDegree_polynomial [Nontrivial R] : W'.polynomial.natDegree = 2 := by
  rw [polynomial_eq]
  exact Cubic.natDegree_of_b_ne_zero' one_ne_zero

lemma monic_polynomial : W'.polynomial.Monic := by
  nontriviality R
  simpa only [polynomial_eq] using Cubic.monic_of_b_eq_one'

lemma irreducible_polynomial [IsDomain R] : Irreducible W'.polynomial := by
  by_contra h
  rcases (monic_polynomial.not_irreducible_iff_exists_add_mul_eq_coeff natDegree_polynomial).mp h
    with ‚ü®f, g, h0, h1‚ü©
  simp only [polynomial_eq, Cubic.coeff_eq_c, Cubic.coeff_eq_d] at h0 h1
  apply_fun degree at h0 h1
  rw [Cubic.degree_of_a_ne_zero' <| neg_ne_zero.mpr <| one_ne_zero' R, degree_mul] at h0
  apply (h1.symm.le.trans Cubic.degree_of_b_eq_zero').not_lt
  rcases Nat.WithBot.add_eq_three_iff.mp h0.symm with h | h | h | h
  iterate 2 rw [degree_add_eq_right_of_degree_lt] <;> simp only [h] <;> decide
  iterate 2 rw [degree_add_eq_left_of_degree_lt] <;> simp only [h] <;> decide

lemma evalEval_polynomial (x y : R) : W'.polynomial.evalEval x y =
    y ^ 2 + W'.a‚ÇÅ * x * y + W'.a‚ÇÉ * y - (x ^ 3 + W'.a‚ÇÇ * x ^ 2 + W'.a‚ÇÑ * x + W'.a‚ÇÜ) := by
  simp only [polynomial]
  eval_simp
  rw [add_mul, ‚Üê add_assoc]

@[simp]
lemma evalEval_polynomial_zero : W'.polynomial.evalEval 0 0 = -W'.a‚ÇÜ := by
  simp only [evalEval_polynomial, zero_add, zero_sub, mul_zero, zero_pow <| Nat.succ_ne_zero _]

variable (W') in
/-- The proposition that an affine point `(x, y)` lies in a Weierstrass curve `W`.

In other words, it satisfies the Weierstrass equation `W(X, Y) = 0`. -/
def Equation (x y : R) : Prop :=
  W'.polynomial.evalEval x y = 0

lemma equation_iff' (x y : R) : W'.Equation x y ‚Üî
    y ^ 2 + W'.a‚ÇÅ * x * y + W'.a‚ÇÉ * y - (x ^ 3 + W'.a‚ÇÇ * x ^ 2 + W'.a‚ÇÑ * x + W'.a‚ÇÜ) = 0 := by
  rw [Equation, evalEval_polynomial]

lemma equation_iff (x y : R) : W'.Equation x y ‚Üî
    y ^ 2 + W'.a‚ÇÅ * x * y + W'.a‚ÇÉ * y = x ^ 3 + W'.a‚ÇÇ * x ^ 2 + W'.a‚ÇÑ * x + W'.a‚ÇÜ := by
  rw [equation_iff', sub_eq_zero]

@[simp]
lemma equation_zero : W'.Equation 0 0 ‚Üî W'.a‚ÇÜ = 0 := by
  rw [Equation, evalEval_polynomial_zero, neg_eq_zero]

lemma equation_iff_variableChange (x y : R) :
    W'.Equation x y ‚Üî (W'.variableChange ‚ü®1, x, 0, y‚ü©).toAffine.Equation 0 0 := by
  rw [equation_iff', ‚Üê neg_eq_zero, equation_zero, variableChange_a‚ÇÜ, inv_one, Units.val_one]
  congr! 1
  ring1

end Equation

section Nonsingular

/-! ### Nonsingular Weierstrass equations -/

variable (W') in
/-- The partial derivative `W_X(X, Y)` with respect to `X` of the polynomial `W(X, Y)` associated to
a Weierstrass curve `W` in affine coordinates. -/
-- TODO: define this in terms of `Polynomial.derivative`.
noncomputable def polynomialX : R[X][Y] :=
  C (C W'.a‚ÇÅ) * Y - C (C 3 * X ^ 2 + C (2 * W'.a‚ÇÇ) * X + C W'.a‚ÇÑ)

lemma evalEval_polynomialX (x y : R) :
    W'.polynomialX.evalEval x y = W'.a‚ÇÅ * y - (3 * x ^ 2 + 2 * W'.a‚ÇÇ * x + W'.a‚ÇÑ) := by
  simp only [polynomialX]
  eval_simp

@[simp]
lemma evalEval_polynomialX_zero : W'.polynomialX.evalEval 0 0 = -W'.a‚ÇÑ := by
  simp only [evalEval_polynomialX, zero_add, zero_sub, mul_zero, zero_pow <| Nat.succ_ne_zero _]

variable (W') in
/-- The partial derivative `W_Y(X, Y)` with respect to `Y` of the polynomial `W(X, Y)` associated to
a Weierstrass curve `W` in affine coordinates. -/
-- TODO: define this in terms of `Polynomial.derivative`.
noncomputable def polynomialY : R[X][Y] :=
  C (C 2) * Y + C (C W'.a‚ÇÅ * X + C W'.a‚ÇÉ)

lemma evalEval_polynomialY (x y : R) : W'.polynomialY.evalEval x y = 2 * y + W'.a‚ÇÅ * x + W'.a‚ÇÉ := by
  simp only [polynomialY]
  eval_simp
  rw [‚Üê add_assoc]

@[simp]
lemma evalEval_polynomialY_zero : W'.polynomialY.evalEval 0 0 = W'.a‚ÇÉ := by
  simp only [evalEval_polynomialY, zero_add, mul_zero]

variable (W') in
/-- The proposition that an affine point `(x, y)` on a Weierstrass curve `W` is nonsingular.

In other words, either `W_X(x, y) ‚â† 0` or `W_Y(x, y) ‚â† 0`.

Note that this definition is only mathematically accurate for fields. -/
-- TODO: generalise this definition to be mathematically accurate for a larger class of rings.
def Nonsingular (x y : R) : Prop :=
  W'.Equation x y ‚àß (W'.polynomialX.evalEval x y ‚â† 0 ‚à® W'.polynomialY.evalEval x y ‚â† 0)

lemma nonsingular_iff' (x y : R) : W'.Nonsingular x y ‚Üî W'.Equation x y ‚àß
    (W'.a‚ÇÅ * y - (3 * x ^ 2 + 2 * W'.a‚ÇÇ * x + W'.a‚ÇÑ) ‚â† 0 ‚à® 2 * y + W'.a‚ÇÅ * x + W'.a‚ÇÉ ‚â† 0) := by
  rw [Nonsingular, equation_iff', evalEval_polynomialX, evalEval_polynomialY]

lemma nonsingular_iff (x y : R) : W'.Nonsingular x y ‚Üî W'.Equation x y ‚àß
    (W'.a‚ÇÅ * y ‚â† 3 * x ^ 2 + 2 * W'.a‚ÇÇ * x + W'.a‚ÇÑ ‚à® y ‚â† -y - W'.a‚ÇÅ * x - W'.a‚ÇÉ) := by
  rw [nonsingular_iff', sub_ne_zero, ‚Üê sub_ne_zero (a := y)]
  congr! 3
  ring1

@[simp]
lemma nonsingular_zero : W'.Nonsingular 0 0 ‚Üî W'.a‚ÇÜ = 0 ‚àß (W'.a‚ÇÉ ‚â† 0 ‚à® W'.a‚ÇÑ ‚â† 0) := by
  rw [Nonsingular, equation_zero, evalEval_polynomialX_zero, neg_ne_zero, evalEval_polynomialY_zero,
    or_comm]

lemma nonsingular_iff_variableChange (x y : R) :
    W'.Nonsingular x y ‚Üî (W'.variableChange ‚ü®1, x, 0, y‚ü©).toAffine.Nonsingular 0 0 := by
  rw [nonsingular_iff', equation_iff_variableChange, equation_zero, ‚Üê neg_ne_zero, or_comm,
    nonsingular_zero, variableChange_a‚ÇÉ, variableChange_a‚ÇÑ, inv_one, Units.val_one]
  simp only [variableChange]
  congr! 3 <;> ring1

private lemma equation_zero_iff_nonsingular_zero_of_Œî_ne_zero (hŒî : W'.Œî ‚â† 0) :
    W'.Equation 0 0 ‚Üî W'.Nonsingular 0 0 := by
  simp only [equation_zero, nonsingular_zero, iff_self_and]
  contrapose! hŒî
  simp only [b‚ÇÇ, b‚ÇÑ, b‚ÇÜ, b‚Çà, Œî, hŒî]
  ring1

/-- A Weierstrass curve is nonsingular at every point if its discriminant is non-zero. -/
lemma equation_iff_nonsingular_of_Œî_ne_zero {x y : R} (hŒî : W'.Œî ‚â† 0) :
    W'.Equation x y ‚Üî W'.Nonsingular x y := by
  rw [equation_iff_variableChange, nonsingular_iff_variableChange,
    equation_zero_iff_nonsingular_zero_of_Œî_ne_zero <| by
      rwa [variableChange_Œî, inv_one, Units.val_one, one_pow, one_mul]]

/-- An elliptic curve is nonsingular at every point. -/
lemma equation_iff_nonsingular [Nontrivial R] [W'.IsElliptic] {x y : R} :
    W'.toAffine.Equation x y ‚Üî W'.toAffine.Nonsingular x y :=
  W'.toAffine.equation_iff_nonsingular_of_Œî_ne_zero <| W'.coe_Œî' ‚ñ∏ W'.Œî'.ne_zero

@[deprecated (since := "2025-03-01")] alias nonsingular_zero_of_Œî_ne_zero :=
  equation_iff_nonsingular_of_Œî_ne_zero
@[deprecated (since := "2025-03-01")] alias nonsingular_of_Œî_ne_zero :=
  equation_iff_nonsingular_of_Œî_ne_zero
@[deprecated (since := "2025-03-01")] alias nonsingular := equation_iff_nonsingular

end Nonsingular

section Ring

/-! ### Group operation polynomials over a ring -/

variable (W') in
/-- The negation polynomial `-Y - a‚ÇÅX - a‚ÇÉ` associated to the negation of a nonsingular affine point
on a Weierstrass curve. -/
noncomputable def negPolynomial : R[X][Y] :=
  -(Y : R[X][Y]) - C (C W'.a‚ÇÅ * X + C W'.a‚ÇÉ)

lemma Y_sub_polynomialY : Y - W'.polynomialY = W'.negPolynomial := by
  rw [polynomialY, negPolynomial]
  C_simp
  ring1

lemma Y_sub_negPolynomial : Y - W'.negPolynomial = W'.polynomialY := by
  rw [‚Üê Y_sub_polynomialY, sub_sub_cancel]

variable (W') in
/-- The `Y`-coordinate of `-(x, y)` for a nonsingular affine point `(x, y)` on a Weierstrass curve
`W`.

This depends on `W`, and has argument order: `x`, `y`. -/
@[simp]
def negY (x y : R) : R :=
  -y - W'.a‚ÇÅ * x - W'.a‚ÇÉ

lemma negY_negY (x y : R) : W'.negY x (W'.negY x y) = y := by
  simp only [negY]
  ring1

lemma evalEval_negPolynomial (x y : R) : W'.negPolynomial.evalEval x y = W'.negY x y := by
  rw [negY, sub_sub, negPolynomial]
  eval_simp

@[deprecated (since := "2025-03-05")] alias eval_negPolynomial := evalEval_negPolynomial

/-- The line polynomial `‚Ñì(X - x) + y` associated to the line `Y = ‚Ñì(X - x) + y` that passes through
a nonsingular affine point `(x, y)` on a Weierstrass curve `W` with a slope of `‚Ñì`.

This does not depend on `W`, and has argument order: `x`, `y`, `‚Ñì`. -/
noncomputable def linePolynomial (x y ‚Ñì : R) : R[X] :=
  C ‚Ñì * (X - C x) + C y

variable (W') in
/-- The addition polynomial obtained by substituting the line `Y = ‚Ñì(X - x) + y` into the polynomial
`W(X, Y)` associated to a Weierstrass curve `W`. If such a line intersects `W` at another
nonsingular affine point `(x', y')` on `W`, then the roots of this polynomial are precisely `x`,
`x'`, and the `X`-coordinate of the addition of `(x, y)` and `(x', y')`.

This depends on `W`, and has argument order: `x`, `y`, `‚Ñì`. -/
noncomputable def addPolynomial (x y ‚Ñì : R) : R[X] :=
  W'.polynomial.eval <| linePolynomial x y ‚Ñì

lemma C_addPolynomial (x y ‚Ñì : R) : C (W'.addPolynomial x y ‚Ñì) =
    (Y - C (linePolynomial x y ‚Ñì)) * (W'.negPolynomial - C (linePolynomial x y ‚Ñì)) +
      W'.polynomial := by
  rw [addPolynomial, linePolynomial, polynomial, negPolynomial]
  eval_simp
  C_simp
  ring1

lemma addPolynomial_eq (x y ‚Ñì : R) : W'.addPolynomial x y ‚Ñì = -Cubic.toPoly
    ‚ü®1, -‚Ñì ^ 2 - W'.a‚ÇÅ * ‚Ñì + W'.a‚ÇÇ,
      2 * x * ‚Ñì ^ 2 + (W'.a‚ÇÅ * x - 2 * y - W'.a‚ÇÉ) * ‚Ñì + (-W'.a‚ÇÅ * y + W'.a‚ÇÑ),
      -x ^ 2 * ‚Ñì ^ 2 + (2 * x * y + W'.a‚ÇÉ * x) * ‚Ñì - (y ^ 2 + W'.a‚ÇÉ * y - W'.a‚ÇÜ)‚ü© := by
  rw [addPolynomial, linePolynomial, polynomial, Cubic.toPoly]
  eval_simp
  C_simp
  ring1

variable (W') in
/-- The `X`-coordinate of `(x‚ÇÅ, y‚ÇÅ) + (x‚ÇÇ, y‚ÇÇ)` for two nonsingular affine points `(x‚ÇÅ, y‚ÇÅ)` and
`(x‚ÇÇ, y‚ÇÇ)` on a Weierstrass curve `W`, where the line through them has a slope of `‚Ñì`.

This depends on `W`, and has argument order: `x‚ÇÅ`, `x‚ÇÇ`, `‚Ñì`. -/
@[simp]
def addX (x‚ÇÅ x‚ÇÇ ‚Ñì : R) : R :=
  ‚Ñì ^ 2 + W'.a‚ÇÅ * ‚Ñì - W'.a‚ÇÇ - x‚ÇÅ - x‚ÇÇ

variable (W') in
/-- The `Y`-coordinate of `-((x‚ÇÅ, y‚ÇÅ) + (x‚ÇÇ, y‚ÇÇ))` for two nonsingular affine points `(x‚ÇÅ, y‚ÇÅ)` and
`(x‚ÇÇ, y‚ÇÇ)` on a Weierstrass curve `W`, where the line through them has a slope of `‚Ñì`.

This depends on `W`, and has argument order: `x‚ÇÅ`, `x‚ÇÇ`, `y‚ÇÅ`, `‚Ñì`. -/
@[simp]
def negAddY (x‚ÇÅ x‚ÇÇ y‚ÇÅ ‚Ñì : R) : R :=
  ‚Ñì * (W'.addX x‚ÇÅ x‚ÇÇ ‚Ñì - x‚ÇÅ) + y‚ÇÅ

variable (W') in
/-- The `Y`-coordinate of `(x‚ÇÅ, y‚ÇÅ) + (x‚ÇÇ, y‚ÇÇ)` for two nonsingular affine points `(x‚ÇÅ, y‚ÇÅ)` and
`(x‚ÇÇ, y‚ÇÇ)` on a Weierstrass curve `W`, where the line through them has a slope of `‚Ñì`.

This depends on `W`, and has argument order: `x‚ÇÅ`, `x‚ÇÇ`, `y‚ÇÅ`, `‚Ñì`. -/
@[simp]
def addY (x‚ÇÅ x‚ÇÇ y‚ÇÅ ‚Ñì : R) : R :=
  W'.negY (W'.addX x‚ÇÅ x‚ÇÇ ‚Ñì) (W'.negAddY x‚ÇÅ x‚ÇÇ y‚ÇÅ ‚Ñì)

lemma equation_neg (x y : R) : W'.Equation x (W'.negY x y) ‚Üî W'.Equation x y := by
  rw [equation_iff, equation_iff, negY]
  congr! 1
  ring1

@[deprecated (since := "2025-02-01")] alias equation_neg_of := equation_neg
@[deprecated (since := "2025-02-01")] alias equation_neg_iff := equation_neg

lemma nonsingular_neg (x y : R) : W'.Nonsingular x (W'.negY x y) ‚Üî W'.Nonsingular x y := by
  rw [nonsingular_iff, equation_neg, ‚Üê negY, negY_negY, ‚Üê @ne_comm _ y, nonsingular_iff]
  exact and_congr_right' <| (iff_congr not_and_or.symm not_and_or.symm).mpr <|
    not_congr <| and_congr_left fun h => by rw [‚Üê h]

@[deprecated (since := "2025-02-01")] alias nonsingular_neg_of := nonsingular_neg
@[deprecated (since := "2025-02-01")] alias nonsingular_neg_iff := nonsingular_neg

lemma equation_add_iff (x‚ÇÅ x‚ÇÇ y‚ÇÅ ‚Ñì : R) : W'.Equation (W'.addX x‚ÇÅ x‚ÇÇ ‚Ñì) (W'.negAddY x‚ÇÅ x‚ÇÇ y‚ÇÅ ‚Ñì) ‚Üî
    (W'.addPolynomial x‚ÇÅ y‚ÇÅ ‚Ñì).eval (W'.addX x‚ÇÅ x‚ÇÇ ‚Ñì) = 0 := by
  rw [Equation, negAddY, addPolynomial, linePolynomial, polynomial]
  eval_simp

lemma nonsingular_negAdd_of_eval_derivative_ne_zero {x‚ÇÅ x‚ÇÇ y‚ÇÅ ‚Ñì : R}
    (hx' : W'.Equation (W'.addX x‚ÇÅ x‚ÇÇ ‚Ñì) (W'.negAddY x‚ÇÅ x‚ÇÇ y‚ÇÅ ‚Ñì))
    (hx : (W'.addPolynomial x‚ÇÅ y‚ÇÅ ‚Ñì).derivative.eval (W'.addX x‚ÇÅ x‚ÇÇ ‚Ñì) ‚â† 0) :
    W'.Nonsingular (W'.addX x‚ÇÅ x‚ÇÇ ‚Ñì) (W'.negAddY x‚ÇÅ x‚ÇÇ y‚ÇÅ ‚Ñì) := by
  rw [Nonsingular, and_iff_right hx', negAddY, polynomialX, polynomialY]
  eval_simp
  contrapose! hx
  rw [addPolynomial, linePolynomial, polynomial]
  eval_simp
  derivative_simp
  simp only [zero_add, add_zero, sub_zero, zero_mul, mul_one]
  eval_simp
  linear_combination (norm := (norm_num1; ring1)) hx.left + ‚Ñì * hx.right

end Ring

section Field

/-! ### Group operation polynomials over a field -/

open Classical in
variable (W) in
/-- The slope of the line through two nonsingular affine points `(x‚ÇÅ, y‚ÇÅ)` and `(x‚ÇÇ, y‚ÇÇ)` on a
Weierstrass curve `W`.

If `x‚ÇÅ ‚â† x‚ÇÇ`, then this line is the secant of `W` through `(x‚ÇÅ, y‚ÇÅ)` and `(x‚ÇÇ, y‚ÇÇ)`, and has slope
`(y‚ÇÅ - y‚ÇÇ) / (x‚ÇÅ - x‚ÇÇ)`. Otherwise, if `y‚ÇÅ ‚â† -y‚ÇÅ - a‚ÇÅx‚ÇÅ - a‚ÇÉ`, then this line is the tangent of `W`
at `(x‚ÇÅ, y‚ÇÅ) = (x‚ÇÇ, y‚ÇÇ)`, and has slope `(3x‚ÇÅ¬≤ + 2a‚ÇÇx‚ÇÅ + a‚ÇÑ - a‚ÇÅy‚ÇÅ) / (2y‚ÇÅ + a‚ÇÅx‚ÇÅ + a‚ÇÉ)`. Otherwise,
this line is vertical, in which case this returns the value `0`.

This depends on `W`, and has argument order: `x‚ÇÅ`, `x‚ÇÇ`, `y‚ÇÅ`, `y‚ÇÇ`. -/
noncomputable def slope (x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F) : F :=
  if x‚ÇÅ = x‚ÇÇ then if y‚ÇÅ = W.negY x‚ÇÇ y‚ÇÇ then 0
    else (3 * x‚ÇÅ ^ 2 + 2 * W.a‚ÇÇ * x‚ÇÅ + W.a‚ÇÑ - W.a‚ÇÅ * y‚ÇÅ) / (y‚ÇÅ - W.negY x‚ÇÅ y‚ÇÅ)
  else (y‚ÇÅ - y‚ÇÇ) / (x‚ÇÅ - x‚ÇÇ)

@[simp]
lemma slope_of_Y_eq {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} (hx : x‚ÇÅ = x‚ÇÇ) (hy : y‚ÇÅ = W.negY x‚ÇÇ y‚ÇÇ) :
    W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ = 0 := by
  rw [slope, if_pos hx, if_pos hy]

@[simp]
lemma slope_of_Y_ne {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} (hx : x‚ÇÅ = x‚ÇÇ) (hy : y‚ÇÅ ‚â† W.negY x‚ÇÇ y‚ÇÇ) :
    W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ =
      (3 * x‚ÇÅ ^ 2 + 2 * W.a‚ÇÇ * x‚ÇÅ + W.a‚ÇÑ - W.a‚ÇÅ * y‚ÇÅ) / (y‚ÇÅ - W.negY x‚ÇÅ y‚ÇÅ) := by
  rw [slope, if_pos hx, if_neg hy]

@[simp]
lemma slope_of_X_ne {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} (hx : x‚ÇÅ ‚â† x‚ÇÇ) :
    W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ = (y‚ÇÅ - y‚ÇÇ) / (x‚ÇÅ - x‚ÇÇ) := by
  rw [slope, if_neg hx]

lemma slope_of_Y_ne_eq_evalEval {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} (hx : x‚ÇÅ = x‚ÇÇ) (hy : y‚ÇÅ ‚â† W.negY x‚ÇÇ y‚ÇÇ) :
    W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ = -W.polynomialX.evalEval x‚ÇÅ y‚ÇÅ / W.polynomialY.evalEval x‚ÇÅ y‚ÇÅ := by
  rw [slope_of_Y_ne hx hy, evalEval_polynomialX, neg_sub]
  congr 1
  rw [negY, evalEval_polynomialY]
  ring1

@[deprecated (since := "2025-03-05")] alias slope_of_Y_ne_eq_eval := slope_of_Y_ne_eq_evalEval

lemma Y_eq_of_X_eq {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} (h‚ÇÅ : W.Equation x‚ÇÅ y‚ÇÅ) (h‚ÇÇ : W.Equation x‚ÇÇ y‚ÇÇ)
    (hx : x‚ÇÅ = x‚ÇÇ) : y‚ÇÅ = y‚ÇÇ ‚à® y‚ÇÅ = W.negY x‚ÇÇ y‚ÇÇ := by
  rw [equation_iff] at h‚ÇÅ h‚ÇÇ
  rw [‚Üê sub_eq_zero, ‚Üê sub_eq_zero (a := y‚ÇÅ), ‚Üê mul_eq_zero, negY]
  linear_combination (norm := (rw [hx]; ring1)) h‚ÇÅ - h‚ÇÇ

lemma Y_eq_of_Y_ne {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} (h‚ÇÅ : W.Equation x‚ÇÅ y‚ÇÅ) (h‚ÇÇ : W.Equation x‚ÇÇ y‚ÇÇ) (hx : x‚ÇÅ = x‚ÇÇ)
    (hy : y‚ÇÅ ‚â† W.negY x‚ÇÇ y‚ÇÇ) : y‚ÇÅ = y‚ÇÇ :=
  (Y_eq_of_X_eq h‚ÇÅ h‚ÇÇ hx).resolve_right hy

lemma addPolynomial_slope {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} (h‚ÇÅ : W.Equation x‚ÇÅ y‚ÇÅ) (h‚ÇÇ : W.Equation x‚ÇÇ y‚ÇÇ)
    (hxy : ¬¨(x‚ÇÅ = x‚ÇÇ ‚àß y‚ÇÅ = W.negY x‚ÇÇ y‚ÇÇ)) : W.addPolynomial x‚ÇÅ y‚ÇÅ (W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) =
      -((X - C x‚ÇÅ) * (X - C x‚ÇÇ) * (X - C (W.addX x‚ÇÅ x‚ÇÇ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ))) := by
  rw [addPolynomial_eq, neg_inj, Cubic.prod_X_sub_C_eq, Cubic.toPoly_injective]
  by_cases hx : x‚ÇÅ = x‚ÇÇ
  ¬∑ have hy : y‚ÇÅ ‚â† W.negY x‚ÇÇ y‚ÇÇ := fun h => hxy ‚ü®hx, h‚ü©
    rcases hx, Y_eq_of_Y_ne h‚ÇÅ h‚ÇÇ hx hy with ‚ü®rfl, rfl‚ü©
    rw [equation_iff] at h‚ÇÅ h‚ÇÇ
    rw [slope_of_Y_ne rfl hy]
    rw [negY, ‚Üê sub_ne_zero] at hy
    ext
    ¬∑ rfl
    ¬∑ simp only [addX]
      ring1
    ¬∑ field_simp [hy]
      ring1
    ¬∑ linear_combination (norm := (field_simp [hy]; ring1)) -h‚ÇÅ
  ¬∑ rw [equation_iff] at h‚ÇÅ h‚ÇÇ
    rw [slope_of_X_ne hx]
    rw [‚Üê sub_eq_zero] at hx
    ext
    ¬∑ rfl
    ¬∑ simp only [addX]
      ring1
    ¬∑ apply mul_right_injective‚ÇÄ hx
      linear_combination (norm := (field_simp [hx]; ring1)) h‚ÇÇ - h‚ÇÅ
    ¬∑ apply mul_right_injective‚ÇÄ hx
      linear_combination (norm := (field_simp [hx]; ring1)) x‚ÇÇ * h‚ÇÅ - x‚ÇÅ * h‚ÇÇ

/-- The negated addition of two affine points in `W` on a sloped line lies in `W`. -/
lemma equation_negAdd {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} (h‚ÇÅ : W.Equation x‚ÇÅ y‚ÇÅ) (h‚ÇÇ : W.Equation x‚ÇÇ y‚ÇÇ)
    (hxy : ¬¨(x‚ÇÅ = x‚ÇÇ ‚àß y‚ÇÅ = W.negY x‚ÇÇ y‚ÇÇ)) : W.Equation
      (W.addX x‚ÇÅ x‚ÇÇ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) (W.negAddY x‚ÇÅ x‚ÇÇ y‚ÇÅ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) := by
  rw [equation_add_iff, addPolynomial_slope h‚ÇÅ h‚ÇÇ hxy]
  eval_simp
  rw [neg_eq_zero, sub_self, mul_zero]

/-- The addition of two affine points in `W` on a sloped line lies in `W`. -/
lemma equation_add {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} (h‚ÇÅ : W.Equation x‚ÇÅ y‚ÇÅ) (h‚ÇÇ : W.Equation x‚ÇÇ y‚ÇÇ)
    (hxy : ¬¨(x‚ÇÅ = x‚ÇÇ ‚àß y‚ÇÅ = W.negY x‚ÇÇ y‚ÇÇ)) :
    W.Equation (W.addX x‚ÇÅ x‚ÇÇ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) (W.addY x‚ÇÅ x‚ÇÇ y‚ÇÅ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) :=
  (equation_neg ..).mpr <| equation_negAdd h‚ÇÅ h‚ÇÇ hxy

lemma C_addPolynomial_slope {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} (h‚ÇÅ : W.Equation x‚ÇÅ y‚ÇÅ) (h‚ÇÇ : W.Equation x‚ÇÇ y‚ÇÇ)
    (hxy : ¬¨(x‚ÇÅ = x‚ÇÇ ‚àß y‚ÇÅ = W.negY x‚ÇÇ y‚ÇÇ)) : C (W.addPolynomial x‚ÇÅ y‚ÇÅ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) =
      -(C (X - C x‚ÇÅ) * C (X - C x‚ÇÇ) * C (X - C (W.addX x‚ÇÅ x‚ÇÇ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ))) := by
  rw [addPolynomial_slope h‚ÇÅ h‚ÇÇ hxy]
  map_simp

lemma derivative_addPolynomial_slope {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} (h‚ÇÅ : W.Equation x‚ÇÅ y‚ÇÅ)
    (h‚ÇÇ : W.Equation x‚ÇÇ y‚ÇÇ) (hxy : ¬¨(x‚ÇÅ = x‚ÇÇ ‚àß y‚ÇÅ = W.negY x‚ÇÇ y‚ÇÇ)) :
    derivative (W.addPolynomial x‚ÇÅ y‚ÇÅ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) =
      -((X - C x‚ÇÅ) * (X - C x‚ÇÇ) + (X - C x‚ÇÅ) * (X - C (W.addX x‚ÇÅ x‚ÇÇ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ)) +
          (X - C x‚ÇÇ) * (X - C (W.addX x‚ÇÅ x‚ÇÇ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ))) := by
  rw [addPolynomial_slope h‚ÇÅ h‚ÇÇ hxy]
  derivative_simp
  ring1

/-- The negated addition of two nonsingular affine points in `W` on a sloped line is nonsingular. -/
lemma nonsingular_negAdd {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} (h‚ÇÅ : W.Nonsingular x‚ÇÅ y‚ÇÅ) (h‚ÇÇ : W.Nonsingular x‚ÇÇ y‚ÇÇ)
    (hxy : ¬¨(x‚ÇÅ = x‚ÇÇ ‚àß y‚ÇÅ = W.negY x‚ÇÇ y‚ÇÇ)) : W.Nonsingular
      (W.addX x‚ÇÅ x‚ÇÇ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) (W.negAddY x‚ÇÅ x‚ÇÇ y‚ÇÅ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) := by
  by_cases hx‚ÇÅ : W.addX x‚ÇÅ x‚ÇÇ (W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) = x‚ÇÅ
  ¬∑ rwa [negAddY, hx‚ÇÅ, sub_self, mul_zero, zero_add]
  ¬∑ by_cases hx‚ÇÇ : W.addX x‚ÇÅ x‚ÇÇ (W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) = x‚ÇÇ
    ¬∑ by_cases hx : x‚ÇÅ = x‚ÇÇ
      ¬∑ subst hx
        contradiction
      ¬∑ rwa [negAddY, ‚Üê neg_sub, mul_neg, hx‚ÇÇ, slope_of_X_ne hx,
          div_mul_cancel‚ÇÄ _ <| sub_ne_zero_of_ne hx, neg_sub, sub_add_cancel]
    ¬∑ apply nonsingular_negAdd_of_eval_derivative_ne_zero <| equation_negAdd h‚ÇÅ.left h‚ÇÇ.left hxy
      rw [derivative_addPolynomial_slope h‚ÇÅ.left h‚ÇÇ.left hxy]
      eval_simp
      simp only [neg_ne_zero, sub_self, mul_zero, add_zero]
      exact mul_ne_zero (sub_ne_zero_of_ne hx‚ÇÅ) (sub_ne_zero_of_ne hx‚ÇÇ)

/-- The addition of two nonsingular affine points in `W` on a sloped line is nonsingular. -/
lemma nonsingular_add {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} (h‚ÇÅ : W.Nonsingular x‚ÇÅ y‚ÇÅ) (h‚ÇÇ : W.Nonsingular x‚ÇÇ y‚ÇÇ)
    (hxy : ¬¨(x‚ÇÅ = x‚ÇÇ ‚àß y‚ÇÅ = W.negY x‚ÇÇ y‚ÇÇ)) :
    W.Nonsingular (W.addX x‚ÇÅ x‚ÇÇ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) (W.addY x‚ÇÅ x‚ÇÇ y‚ÇÅ <| W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) :=
  (nonsingular_neg ..).mpr <| nonsingular_negAdd h‚ÇÅ h‚ÇÇ hxy

/-- The formula `x(P‚ÇÅ + P‚ÇÇ) = x(P‚ÇÅ - P‚ÇÇ) - œà(P‚ÇÅ)œà(P‚ÇÇ) / (x(P‚ÇÇ) - x(P‚ÇÅ))¬≤`,
where `œà(x,y) = 2y + a‚ÇÅx + a‚ÇÉ`. -/
lemma addX_eq_addX_negY_sub {x‚ÇÅ x‚ÇÇ : F} (y‚ÇÅ y‚ÇÇ : F) (hx : x‚ÇÅ ‚â† x‚ÇÇ) :
    W.addX x‚ÇÅ x‚ÇÇ (W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) = W.addX x‚ÇÅ x‚ÇÇ (W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ <| W.negY x‚ÇÇ y‚ÇÇ) -
      (y‚ÇÅ - W.negY x‚ÇÅ y‚ÇÅ) * (y‚ÇÇ - W.negY x‚ÇÇ y‚ÇÇ) / (x‚ÇÇ - x‚ÇÅ) ^ 2 := by
  simp_rw [slope_of_X_ne hx, addX, negY, ‚Üê neg_sub x‚ÇÅ, neg_sq]
  field_simp [sub_ne_zero.mpr hx]
  ring1

/-- The formula `y(P‚ÇÅ)(x(P‚ÇÇ) - x(P‚ÇÉ)) + y(P‚ÇÇ)(x(P‚ÇÉ) - x(P‚ÇÅ)) + y(P‚ÇÉ)(x(P‚ÇÅ) - x(P‚ÇÇ)) = 0`,
assuming that `P‚ÇÅ + P‚ÇÇ + P‚ÇÉ = O`. -/
lemma cyclic_sum_Y_mul_X_sub_X {x‚ÇÅ x‚ÇÇ : F} (y‚ÇÅ y‚ÇÇ : F) (hx : x‚ÇÅ ‚â† x‚ÇÇ) :
    let x‚ÇÉ := W.addX x‚ÇÅ x‚ÇÇ (W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ)
    y‚ÇÅ * (x‚ÇÇ - x‚ÇÉ) + y‚ÇÇ * (x‚ÇÉ - x‚ÇÅ) + W.negAddY x‚ÇÅ x‚ÇÇ y‚ÇÅ (W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) * (x‚ÇÅ - x‚ÇÇ) = 0 := by
  simp_rw [slope_of_X_ne hx, negAddY, addX]
  field_simp [sub_ne_zero.mpr hx]
  ring1

/-- The formula `œà(P‚ÇÅ + P‚ÇÇ) = (œà(P‚ÇÇ)(x(P‚ÇÅ) - x(P‚ÇÉ)) - œà(P‚ÇÅ)(x(P‚ÇÇ) - x(P‚ÇÉ))) / (x(P‚ÇÇ) - x(P‚ÇÅ))`,
where `œà(x,y) = 2y + a‚ÇÅx + a‚ÇÉ`. -/
lemma addY_sub_negY_addY {x‚ÇÅ x‚ÇÇ : F} (y‚ÇÅ y‚ÇÇ : F) (hx : x‚ÇÅ ‚â† x‚ÇÇ) :
    let x‚ÇÉ := W.addX x‚ÇÅ x‚ÇÇ (W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ)
    let y‚ÇÉ := W.addY x‚ÇÅ x‚ÇÇ y‚ÇÅ (W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ)
    y‚ÇÉ - W.negY x‚ÇÉ y‚ÇÉ =
      ((y‚ÇÇ - W.negY x‚ÇÇ y‚ÇÇ) * (x‚ÇÅ - x‚ÇÉ) - (y‚ÇÅ - W.negY x‚ÇÅ y‚ÇÅ) * (x‚ÇÇ - x‚ÇÉ)) / (x‚ÇÇ - x‚ÇÅ) := by
  simp_rw [addY, negY, eq_div_iff (sub_ne_zero.mpr hx.symm)]
  linear_combination (norm := ring1) 2 * cyclic_sum_Y_mul_X_sub_X y‚ÇÅ y‚ÇÇ hx

end Field

section Group

/-! ### Nonsingular points -/

variable (W') in
/-- A nonsingular point on a Weierstrass curve `W` in affine coordinates. This is either the unique
point at infinity `WeierstrassCurve.Affine.Point.zero` or a nonsingular affine point
`WeierstrassCurve.Affine.Point.some (x, y)` satisfying the Weierstrass equation of `W`. -/
inductive Point
  | zero
  | some {x y : R} (h : W'.Nonsingular x y)

/-- For an algebraic extension `S` of a ring `R`, the type of nonsingular `S`-points on a
Weierstrass curve `W` over `R` in affine coordinates. -/
scoped notation3:max W' "‚üÆ" S "‚üØ" => Affine.Point <| baseChange W' S

namespace Point

/-! ### Group operations -/

instance : Inhabited W'.Point :=
  ‚ü®.zero‚ü©

instance : Zero W'.Point :=
  ‚ü®.zero‚ü©

lemma zero_def : 0 = (.zero : W'.Point) :=
  rfl

lemma some_ne_zero {x y : R} (h : W'.Nonsingular x y) : Point.some h ‚â† 0 := by
  rintro (_ | _)

/-- The negation of a nonsingular point on a Weierstrass curve in affine coordinates.

Given a nonsingular point `P` in affine coordinates, use `-P` instead of `neg P`. -/
def neg : W'.Point ‚Üí W'.Point
  | 0 => 0
  | some h => some <| (nonsingular_neg ..).mpr h

instance : Neg W'.Point :=
  ‚ü®neg‚ü©

lemma neg_def (P : W'.Point) : -P = P.neg :=
  rfl

@[simp]
lemma neg_zero : (-0 : W'.Point) = 0 :=
  rfl

@[simp]
lemma neg_some {x y : R} (h : W'.Nonsingular x y) : -some h = some ((nonsingular_neg ..).mpr h) :=
  rfl

instance : InvolutiveNeg W'.Point where
  neg_neg := by
    rintro (_ | _)
    ¬∑ rfl
    ¬∑ simp only [neg_some, negY_negY]

open Classical in
/-- The addition of two nonsingular points on a Weierstrass curve in affine coordinates.

Given two nonsingular points `P` and `Q` in affine coordinates, use `P + Q` instead of `add P Q`. -/
noncomputable def add : W.Point ‚Üí W.Point ‚Üí W.Point
  | 0, P => P
  | P, 0 => P
  | @some _ _ _ x‚ÇÅ y‚ÇÅ h‚ÇÅ, @some _ _ _ x‚ÇÇ y‚ÇÇ h‚ÇÇ =>
    if hxy : x‚ÇÅ = x‚ÇÇ ‚àß y‚ÇÅ = W.negY x‚ÇÇ y‚ÇÇ then 0 else some <| nonsingular_add h‚ÇÅ h‚ÇÇ hxy

noncomputable instance : Add W.Point :=
  ‚ü®add‚ü©

noncomputable instance : AddZeroClass W.Point :=
  ‚ü®by rintro (_ | _) <;> rfl, by rintro (_ | _) <;> rfl‚ü©

lemma add_def (P Q : W.Point) : P + Q = P.add Q :=
  rfl

lemma add_some {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} (hxy : ¬¨(x‚ÇÅ = x‚ÇÇ ‚àß y‚ÇÅ = W.negY x‚ÇÇ y‚ÇÇ)) {h‚ÇÅ : W.Nonsingular x‚ÇÅ y‚ÇÅ}
    {h‚ÇÇ : W.Nonsingular x‚ÇÇ y‚ÇÇ} : some h‚ÇÅ + some h‚ÇÇ = some (nonsingular_add h‚ÇÅ h‚ÇÇ hxy) := by
  simp only [add_def, add, dif_neg hxy]

@[deprecated (since := "2025-02-28")] alias add_of_imp := add_some

@[simp]
lemma add_of_Y_eq {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} {h‚ÇÅ : W.Nonsingular x‚ÇÅ y‚ÇÅ} {h‚ÇÇ : W.Nonsingular x‚ÇÇ y‚ÇÇ}
    (hx : x‚ÇÅ = x‚ÇÇ) (hy : y‚ÇÅ = W.negY x‚ÇÇ y‚ÇÇ) : some h‚ÇÅ + some h‚ÇÇ = 0 := by
  simpa only [add_def, add] using dif_pos ‚ü®hx, hy‚ü©

@[simp]
lemma add_self_of_Y_eq {x‚ÇÅ y‚ÇÅ : F} {h‚ÇÅ : W.Nonsingular x‚ÇÅ y‚ÇÅ} (hy : y‚ÇÅ = W.negY x‚ÇÅ y‚ÇÅ) :
    some h‚ÇÅ + some h‚ÇÅ = 0 :=
  add_of_Y_eq rfl hy

@[simp]
lemma add_of_Y_ne {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} {h‚ÇÅ : W.Nonsingular x‚ÇÅ y‚ÇÅ} {h‚ÇÇ : W.Nonsingular x‚ÇÇ y‚ÇÇ}
    (hy : y‚ÇÅ ‚â† W.negY x‚ÇÇ y‚ÇÇ) :
    some h‚ÇÅ + some h‚ÇÇ = some (nonsingular_add h‚ÇÅ h‚ÇÇ fun hxy => hy hxy.right) :=
  add_some fun hxy => hy hxy.right

lemma add_of_Y_ne' {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} {h‚ÇÅ : W.Nonsingular x‚ÇÅ y‚ÇÅ} {h‚ÇÇ : W.Nonsingular x‚ÇÇ y‚ÇÇ}
    (hy : y‚ÇÅ ‚â† W.negY x‚ÇÇ y‚ÇÇ) :
    some h‚ÇÅ + some h‚ÇÇ = -some (nonsingular_negAdd h‚ÇÅ h‚ÇÇ fun hxy => hy hxy.right) :=
  add_of_Y_ne hy

@[simp]
lemma add_self_of_Y_ne {x‚ÇÅ y‚ÇÅ : F} {h‚ÇÅ : W.Nonsingular x‚ÇÅ y‚ÇÅ} (hy : y‚ÇÅ ‚â† W.negY x‚ÇÅ y‚ÇÅ) :
    some h‚ÇÅ + some h‚ÇÅ = some (nonsingular_add h‚ÇÅ h‚ÇÅ fun hxy => hy hxy.right) :=
  add_of_Y_ne hy

lemma add_self_of_Y_ne' {x‚ÇÅ y‚ÇÅ : F} {h‚ÇÅ : W.Nonsingular x‚ÇÅ y‚ÇÅ} (hy : y‚ÇÅ ‚â† W.negY x‚ÇÅ y‚ÇÅ) :
    some h‚ÇÅ + some h‚ÇÅ = -some (nonsingular_negAdd h‚ÇÅ h‚ÇÅ fun hxy => hy hxy.right) :=
  add_of_Y_ne hy

@[simp]
lemma add_of_X_ne {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} {h‚ÇÅ : W.Nonsingular x‚ÇÅ y‚ÇÅ} {h‚ÇÇ : W.Nonsingular x‚ÇÇ y‚ÇÇ}
    (hx : x‚ÇÅ ‚â† x‚ÇÇ) : some h‚ÇÅ + some h‚ÇÇ = some (nonsingular_add h‚ÇÅ h‚ÇÇ fun hxy => hx hxy.left) :=
  add_some fun hxy => hx hxy.left

lemma add_of_X_ne' {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F} {h‚ÇÅ : W.Nonsingular x‚ÇÅ y‚ÇÅ} {h‚ÇÇ : W.Nonsingular x‚ÇÇ y‚ÇÇ}
    (hx : x‚ÇÅ ‚â† x‚ÇÇ) : some h‚ÇÅ + some h‚ÇÇ = -some (nonsingular_negAdd h‚ÇÅ h‚ÇÇ fun hxy => hx hxy.left) :=
  add_of_X_ne hx

end Point

end Group

section Map

/-! ### Maps across ring homomorphisms -/

variable (f : R ‚Üí+* S) (x y x‚ÇÅ y‚ÇÅ x‚ÇÇ y‚ÇÇ ‚Ñì : R)

lemma map_polynomial : (W'.map f).toAffine.polynomial = W'.polynomial.map (mapRingHom f) := by
  simp only [polynomial]
  map_simp

lemma evalEval_baseChange_polynomial :
    (W'.baseChange R[X][Y]).toAffine.polynomial.evalEval (C X) Y = W'.polynomial := by
  rw [map_polynomial, evalEval, eval_map, eval_C_X_eval‚ÇÇ_map_C_X]

@[deprecated (since := "2025-03-05")] alias evalEval_baseChange_polynomial_X_Y :=
  evalEval_baseChange_polynomial

variable {x y} in
lemma Equation.map {x y : R} (h : W'.Equation x y) : (W'.map f).toAffine.Equation (f x) (f y) := by
  rw [Equation, map_polynomial, map_mapRingHom_evalEval, h, map_zero]

variable {f} in
lemma map_equation (hf : Function.Injective f) :
    (W'.map f).toAffine.Equation (f x) (f y) ‚Üî W'.Equation x y := by
  simp only [Equation, map_polynomial, map_mapRingHom_evalEval, map_eq_zero_iff f hf]

lemma map_polynomialX : (W'.map f).toAffine.polynomialX = W'.polynomialX.map (mapRingHom f) := by
  simp only [polynomialX]
  map_simp

lemma map_polynomialY : (W'.map f).toAffine.polynomialY = W'.polynomialY.map (mapRingHom f) := by
  simp only [polynomialY]
  map_simp

variable {f} in
lemma map_nonsingular (hf : Function.Injective f) :
    (W'.map f).toAffine.Nonsingular (f x) (f y) ‚Üî W'.Nonsingular x y := by
  simp only [Nonsingular, evalEval, map_equation _ _ hf, map_polynomialX, map_polynomialY,
    map_mapRingHom_evalEval, map_ne_zero_iff f hf]

lemma map_negPolynomial :
    (W'.map f).toAffine.negPolynomial = W'.negPolynomial.map (mapRingHom f) := by
  simp only [negPolynomial]
  map_simp

lemma map_negY : (W'.map f).toAffine.negY (f x) (f y) = f (W'.negY x y) := by
  simp only [negY]
  map_simp

lemma map_linePolynomial : linePolynomial (f x) (f y) (f ‚Ñì) = (linePolynomial x y ‚Ñì).map f := by
  simp only [linePolynomial]
  map_simp

lemma map_addPolynomial :
    (W'.map f).toAffine.addPolynomial (f x) (f y) (f ‚Ñì) = (W'.addPolynomial x y ‚Ñì).map f := by
  rw [addPolynomial, map_polynomial, eval_map, linePolynomial, addPolynomial, ‚Üê coe_mapRingHom,
    ‚Üê eval‚ÇÇ_hom, linePolynomial]
  map_simp

lemma map_addX : (W'.map f).toAffine.addX (f x‚ÇÅ) (f x‚ÇÇ) (f ‚Ñì) = f (W'.addX x‚ÇÅ x‚ÇÇ ‚Ñì) := by
  simp only [addX]
  map_simp

lemma map_negAddY :
    (W'.map f).toAffine.negAddY (f x‚ÇÅ) (f x‚ÇÇ) (f y‚ÇÅ) (f ‚Ñì) = f (W'.negAddY x‚ÇÅ x‚ÇÇ y‚ÇÅ ‚Ñì) := by
  simp only [negAddY, map_addX]
  map_simp

lemma map_addY :
    (W'.map f).toAffine.addY (f x‚ÇÅ) (f x‚ÇÇ) (f y‚ÇÅ) (f ‚Ñì) = f (W'.toAffine.addY x‚ÇÅ x‚ÇÇ y‚ÇÅ ‚Ñì) := by
  simp only [addY, map_negAddY, map_addX, map_negY]

lemma map_slope (f : F ‚Üí+* K) (x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F) :
    (W.map f).toAffine.slope (f x‚ÇÅ) (f x‚ÇÇ) (f y‚ÇÅ) (f y‚ÇÇ) = f (W.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) := by
  by_cases hx : x‚ÇÅ = x‚ÇÇ
  ¬∑ by_cases hy : y‚ÇÅ = W.negY x‚ÇÇ y‚ÇÇ
    ¬∑ rw [slope_of_Y_eq (congr_arg f hx) <| by rw [hy, map_negY], slope_of_Y_eq hx hy, map_zero]
    ¬∑ rw [slope_of_Y_ne (congr_arg f hx) <| map_negY f x‚ÇÇ y‚ÇÇ ‚ñ∏ fun h => hy <| f.injective h,
        map_negY, slope_of_Y_ne hx hy]
      map_simp
  ¬∑ rw [slope_of_X_ne fun h => hx <| f.injective h, slope_of_X_ne hx]
    map_simp

end Map

section BaseChange

/-! ### Base changes across algebra homomorphisms -/

variable [Algebra R S] [Algebra R A] [Algebra S A] [IsScalarTower R S A] [Algebra R B] [Algebra S B]
  [IsScalarTower R S B] (f : A ‚Üí‚Çê[S] B) (x y x‚ÇÅ y‚ÇÅ x‚ÇÇ y‚ÇÇ ‚Ñì : A)

lemma baseChange_polynomial : (W'.baseChange B).toAffine.polynomial =
    (W'.baseChange A).toAffine.polynomial.map (mapRingHom f) := by
  rw [‚Üê map_polynomial, map_baseChange]

variable {x y} in
lemma Equation.baseChange (h : (W'.baseChange A).toAffine.Equation x y) :
    (W'.baseChange B).toAffine.Equation (f x) (f y) := by
  convert Equation.map f.toRingHom h using 1
  rw [AlgHom.toRingHom_eq_coe, map_baseChange]

variable {f} in
lemma baseChange_equation (hf : Function.Injective f) :
    (W'.baseChange B).toAffine.Equation (f x) (f y) ‚Üî (W'.baseChange A).toAffine.Equation x y := by
  rw [‚Üê map_equation _ _ hf, AlgHom.toRingHom_eq_coe, map_baseChange, RingHom.coe_coe]

lemma baseChange_polynomialX : (W'.baseChange B).toAffine.polynomialX =
    (W'.baseChange A).toAffine.polynomialX.map (mapRingHom f) := by
  rw [‚Üê map_polynomialX, map_baseChange]

lemma baseChange_polynomialY : (W'.baseChange B).toAffine.polynomialY =
    (W'.baseChange A).toAffine.polynomialY.map (mapRingHom f) := by
  rw [‚Üê map_polynomialY, map_baseChange]

variable {f} in
lemma baseChange_nonsingular (hf : Function.Injective f) :
    (W'.baseChange B).toAffine.Nonsingular (f x) (f y) ‚Üî
      (W'.baseChange A).toAffine.Nonsingular x y := by
  rw [‚Üê map_nonsingular _ _ hf, AlgHom.toRingHom_eq_coe, map_baseChange, RingHom.coe_coe]

lemma baseChange_negPolynomial : (W'.baseChange B).toAffine.negPolynomial =
    (W'.baseChange A).toAffine.negPolynomial.map (mapRingHom f) := by
  rw [‚Üê map_negPolynomial, map_baseChange]

lemma baseChange_negY :
    (W'.baseChange B).toAffine.negY (f x) (f y) = f ((W'.baseChange A).toAffine.negY x y) := by
  rw [‚Üê RingHom.coe_coe, ‚Üê map_negY, map_baseChange]

lemma baseChange_addPolynomial : (W'.baseChange B).toAffine.addPolynomial (f x) (f y) (f ‚Ñì) =
    ((W'.baseChange A).toAffine.addPolynomial x y ‚Ñì).map f := by
  rw [‚Üê RingHom.coe_coe, ‚Üê map_addPolynomial, map_baseChange]

lemma baseChange_addX : (W'.baseChange B).toAffine.addX (f x‚ÇÅ) (f x‚ÇÇ) (f ‚Ñì) =
    f ((W'.baseChange A).toAffine.addX x‚ÇÅ x‚ÇÇ ‚Ñì) := by
  rw [‚Üê RingHom.coe_coe, ‚Üê map_addX, map_baseChange]

lemma baseChange_negAddY : (W'.baseChange B).toAffine.negAddY (f x‚ÇÅ) (f x‚ÇÇ) (f y‚ÇÅ) (f ‚Ñì) =
    f ((W'.baseChange A).toAffine.negAddY x‚ÇÅ x‚ÇÇ y‚ÇÅ ‚Ñì) := by
  rw [‚Üê RingHom.coe_coe, ‚Üê map_negAddY, map_baseChange]

lemma baseChange_addY : (W'.baseChange B).toAffine.addY (f x‚ÇÅ) (f x‚ÇÇ) (f y‚ÇÅ) (f ‚Ñì) =
    f ((W'.baseChange A).toAffine.addY x‚ÇÅ x‚ÇÇ y‚ÇÅ ‚Ñì) := by
  rw [‚Üê RingHom.coe_coe, ‚Üê map_addY, map_baseChange]

lemma baseChange_slope [Algebra R F] [Algebra S F] [IsScalarTower R S F] [Algebra R K] [Algebra S K]
  [IsScalarTower R S K] (f : F ‚Üí‚Çê[S] K) (x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : F) :
  (W'.baseChange K).toAffine.slope (f x‚ÇÅ) (f x‚ÇÇ) (f y‚ÇÅ) (f y‚ÇÇ) =
    f ((W'.baseChange F).toAffine.slope x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ) := by
  rw [‚Üê RingHom.coe_coe, ‚Üê map_slope, map_baseChange]

end BaseChange

namespace Point

variable [Algebra R S] [Algebra R F] [Algebra S F] [IsScalarTower R S F] [Algebra R K] [Algebra S K]
  [IsScalarTower R S K] [Algebra R L] [Algebra S L] [IsScalarTower R S L] (f : F ‚Üí‚Çê[S] K)
  (g : K ‚Üí‚Çê[S] L)

/-- The group homomorphism from `W‚üÆF‚üØ` to `W‚üÆK‚üØ` induced by an algebra homomorphism `f : F ‚Üí‚Çê[S] K`,
where `W` is defined over a subring of a ring `S`, and `F` and `K` are field extensions of `S`. -/
noncomputable def map : W'‚üÆF‚üØ ‚Üí+ W'‚üÆK‚üØ where
  toFun P := match P with
    | 0 => 0
    | some h => some <| (baseChange_nonsingular _ _ f.injective).mpr h
  map_zero' := rfl
  map_add' := by
    rintro (_ | @‚ü®x‚ÇÅ, y‚ÇÅ, _‚ü©) (_ | @‚ü®x‚ÇÇ, y‚ÇÇ, _‚ü©)
    any_goals rfl
    by_cases hxy : x‚ÇÅ = x‚ÇÇ ‚àß y‚ÇÅ = (W'.baseChange F).toAffine.negY x‚ÇÇ y‚ÇÇ
    ¬∑ simp only [add_of_Y_eq hxy.left hxy.right]
      rw [add_of_Y_eq (congr_arg _ hxy.left) <| by rw [hxy.right, baseChange_negY]]
    ¬∑ simp only [add_some hxy, ‚Üê baseChange_addX, ‚Üê baseChange_addY, ‚Üê baseChange_slope]
      rw [add_some fun h => hxy ‚ü®f.injective h.1, f.injective (W'.baseChange_negY f .. ‚ñ∏ h).2‚ü©]

@[deprecated (since := "2025-03-01")] alias mapFun := map

lemma map_zero : map f (0 : W'‚üÆF‚üØ) = 0 :=
  rfl

lemma map_some {x y : F} (h : (W'.baseChange F).toAffine.Nonsingular x y) :
    map f (some h) = some ((W'.baseChange_nonsingular _ _ f.injective).mpr h) :=
  rfl

lemma map_id (P : W'‚üÆF‚üØ) : map (Algebra.ofId F F) P = P := by
  cases P <;> rfl

lemma map_map (P : W'‚üÆF‚üØ) : map g (map f P) = map (g.comp f) P := by
  cases P <;> rfl

lemma map_injective : Function.Injective <| map (W' := W') f := by
  rintro (_ | _) (_ | _) h
  any_goals contradiction
  ¬∑ rfl
  ¬∑ simpa only [some.injEq] using ‚ü®f.injective (some.inj h).left, f.injective (some.inj h).right‚ü©

variable (F K) in
/-- The group homomorphism from `W‚üÆF‚üØ` to `W‚üÆK‚üØ` induced by the base change from `F` to `K`, where
`W` is defined over a subring of a ring `S`, and `F` and `K` are field extensions of `S`. -/
noncomputable abbrev baseChange [Algebra F K] [IsScalarTower R F K] : W'‚üÆF‚üØ ‚Üí+ W'‚üÆK‚üØ :=
  map <| Algebra.ofId F K

lemma map_baseChange [Algebra F K] [IsScalarTower R F K] [Algebra F L] [IsScalarTower R F L]
    (f : K ‚Üí‚Çê[F] L) (P : W'‚üÆF‚üØ) : map f (baseChange F K P) = baseChange F L P := by
  have : Subsingleton (F ‚Üí‚Çê[F] L) := inferInstance
  convert map_map (Algebra.ofId F K) f P

end Point

end Affine

end WeierstrassCurve
