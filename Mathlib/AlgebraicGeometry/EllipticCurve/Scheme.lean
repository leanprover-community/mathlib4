/-
Copyright (c) 2024 Junyan Xu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Junyan Xu
-/
import Mathlib.AlgebraicGeometry.EllipticCurve.Group
import Mathlib.AlgebraicGeometry.GammaSpecAdjunction
import Mathlib.AlgebraicGeometry.Gluing

/-!
-/

universe u

section algebra_schemeOver
-- TODO: move these

variable (R A A') [CommRing R] [CommRing A] [CommRing A'] [Algebra R A] [Algebra R A']

open CategoryTheory Opposite AlgebraicGeometry

/-- -/
def Algebra.mkOver : Over (op <| CommRingCat.of R) :=
  .mk (op <| CommRingCat.ofHom <| algebraMap R A)

/-- -/
def AlgHom.equivHomOver : (A ‚Üí‚Çê[R] A') ‚âÉ (Algebra.mkOver R A' ‚ü∂ Algebra.mkOver R A) where
  toFun f := Over.homMk (op f.toRingHom) (unop_injective f.comp_algebraMap)
  invFun f := .mk f.left.unop fun r ‚Ü¶ congr(Quiver.Hom.unop $(Over.w f) r)
  left_inv f := by ext1; simp
  right_inv f := by simp; rfl

variable {C D} [Category C] [Category D] (F : C ‚•§ D)
/-- -/
@[simps!] def CategoryTheory.Functor.mapOver (c : C) : Over c ‚•§ Over (F.obj c) :=
  Comma.map (F‚ÇÅ := F) (F‚ÇÇ := ùü≠ _) (F := F) (ùüô _) { app := fun _ ‚Ü¶ ùüô _ }

@[simp] lemma CategoryTheory.Functor.mapOver_hom (c : C) (c' : Over c) :
    ((F.mapOver c).obj c').hom = F.map c'.hom := by simp

/-- -/
noncomputable def Algebra.schemeSpecOver : Over (Scheme.Spec.obj <| op <| CommRingCat.of R) :=
  (Scheme.Spec.mapOver _).obj (Algebra.mkOver R A)

variable {F} in
/-- -/
def CategoryTheory.Functor.FullyFaithful.mapOver (ff : F.FullyFaithful) (c : C) :
    (F.mapOver c).FullyFaithful where
  preimage f := Over.homMk (ff.preimage f.left) (ff.map_injective <| by simpa using Over.w f)

/-- -/
noncomputable def AlgHom.equivSchemeOver :
    (A ‚Üí‚Çê[R] A') ‚âÉ (Algebra.schemeSpecOver R A' ‚ü∂ Algebra.schemeSpecOver R A) :=
  (AlgHom.equivHomOver R A A').trans (Spec.fullyFaithful.mapOver _).homEquiv

end algebra_schemeOver

inductive PBool : Type u | false | true

namespace CategoryTheory

variable {C : Type*} [Category C]

open Limits
attribute [local instance] hasPullback_of_left_iso hasPullback_of_right_iso

-- should we generalize J to arbitrary universe? and pullback to arbitrary IsPullback?
-- TODO: the name CategoryTheory.Limits.pullback_snd_iso_of_right_iso is wrong (should be fst)

@[reassoc, simp]
lemma pullback_snd_comp_inv_fst {c‚ÇÅ c‚ÇÇ c : C} {f‚ÇÅ : c‚ÇÅ ‚ü∂ c} {f‚ÇÇ : c‚ÇÇ ‚ü∂ c} [IsIso f‚ÇÅ] :
    pullback.snd ‚â´ inv pullback.fst = (pullbackSymmetry f‚ÇÅ f‚ÇÇ).hom := by
  rw [IsIso.comp_inv_eq, pullbackSymmetry_hom_comp_fst]

suppress_compilation

/-- Construct a glue datum from a two monomorphisms with the same source. -/
@[simps] def GlueData.mk‚ÇÇ {U‚ÇÅ U‚ÇÇ V : C} (f‚ÇÅ : V ‚ü∂ U‚ÇÅ) (f‚ÇÇ : V ‚ü∂ U‚ÇÇ) [Mono f‚ÇÅ] [Mono f‚ÇÇ] :
    GlueData C where
  J := PBool
  U := PBool.rec U‚ÇÅ U‚ÇÇ
  V b := b.1.rec (b.2.rec U‚ÇÅ V) (b.2.rec V U‚ÇÇ)
  f := PBool.rec (PBool.rec (ùüô U‚ÇÅ) f‚ÇÅ) (PBool.rec f‚ÇÇ (ùüô U‚ÇÇ))
  f_mono := by rintro ‚ü®_‚ü© ‚ü®_‚ü© <;> infer_instance
  f_hasPullback := by rintro ‚ü®_‚ü© ‚ü®_‚ü© ‚ü®_‚ü© <;> dsimp only <;> infer_instance
  f_id := by rintro ‚ü®_‚ü© <;> infer_instance
  t := PBool.rec (PBool.rec (ùüô U‚ÇÅ) (ùüô V)) (PBool.rec (ùüô V) (ùüô U‚ÇÇ))
  t_id := by rintro ‚ü®_‚ü© <;> rfl
  t' := PBool.rec (PBool.rec (PBool.rec (ùüô _) <| pullback.snd ‚â´ inv pullback.fst) <| PBool.rec
      (pullback.fst ‚â´ inv (pullback.fst (f := f‚ÇÇ))) <| pullback.fst (f := f‚ÇÅ) ‚â´ inv pullback.snd)
    (PBool.rec (PBool.rec (pullback.fst (f := f‚ÇÇ) ‚â´ inv pullback.snd) <| pullback.fst ‚â´
      inv (pullback.fst (f := f‚ÇÅ))) <| PBool.rec (pullback.snd ‚â´ inv pullback.fst) <| ùüô _)
    -- two out of eight can use pullbackSymmetry but this is easier for cocycle
  t_fac := by rintro ‚ü®_‚ü© ‚ü®_‚ü© ‚ü®_‚ü© <;> simp [fst_eq_snd_of_mono_eq]
  cocycle := by rintro ‚ü®_‚ü© ‚ü®_‚ü© ‚ü®_‚ü© <;>
    simp only [Category.assoc, IsIso.inv_hom_id_assoc, IsIso.hom_inv_id, Category.comp_id]

end CategoryTheory

namespace AlgebraicGeometry.Scheme

/-- Construct a glue datum from two open immersions with the same source. -/
@[simps!] noncomputable def GlueData.mk‚ÇÇ {U‚ÇÅ U‚ÇÇ V : Scheme} (f‚ÇÅ : V ‚ü∂ U‚ÇÅ) (f‚ÇÇ : V ‚ü∂ U‚ÇÇ)
    [IsOpenImmersion f‚ÇÅ] [IsOpenImmersion f‚ÇÇ] : GlueData where
  __ := CategoryTheory.GlueData.mk‚ÇÇ f‚ÇÅ f‚ÇÇ
  f_open := by rintro ‚ü®_‚ü© ‚ü®_‚ü© <;> dsimp only [CategoryTheory.GlueData.mk‚ÇÇ_f] <;> infer_instance

end AlgebraicGeometry.Scheme

/-! ### The coordinate ring at infinity -/

namespace WeierstrassCurve.Projective

noncomputable section

open Polynomial AlgebraicGeometry

variable {R : Type u} [CommRing R] (W : WeierstrassCurve R)

/-- The equation of the Weierstrass curve at infinity. -/
def polynomialInf : R[X][Y] :=
  letI x : R[X][Y] := C X;
  -x ^ 3 + Y * (1 + CC W.a‚ÇÅ * x - CC W.a‚ÇÇ * x ^ 2 + CC W.a‚ÇÉ * Y - CC W.a‚ÇÑ * x * Y - CC W.a‚ÇÜ * Y ^ 2)

def polynomialZ : R[X][Y] :=
  1 + C (C W.a‚ÇÅ * X - C W.a‚ÇÇ * X ^ 2) + 2 * C (C W.a‚ÇÉ - C W.a‚ÇÑ * X) * Y - 3 * CC W.a‚ÇÜ * Y ^ 2

lemma eval‚ÇÇ_polynomialInf {S} [CommRing S] [Algebra R S] (f : R[X] ‚Üí‚Çê[R] S)
    (x y z : S) (hx : x * z = f X) (hy : y * z = 1) :
    eval‚ÇÇ f z (polynomialInf W) = z ^ 3 * aevalAEval x y (Affine.polynomial W) := by
  rw [‚Üê coe_eval‚ÇÇRingHom]
  simp only [polynomialInf, map_add, map_neg, map_pow, coe_eval‚ÇÇRingHom, eval‚ÇÇ_C, RingHom.coe_coe,
    map_mul, eval‚ÇÇ_X, map_sub, map_one, Affine.polynomial, aevalAEval_apply, aeval_C, aeval_X]
  conv in _ * z => rw [‚Üê one_mul z]
  conv in 1 + _ => rw [‚Üê one_pow 2]
  conv in _ * f X => rw [‚Üê one_mul (f X)]
  rw [‚Üê hx, ‚Üê hy]
  simp_rw [C_eq_algebraMap, AlgHom.commutes]
  ring

lemma eval‚ÇÇ_affine_polynomial {S} [CommRing S] [Algebra R S] (f : R[X] ‚Üí‚Çê[R] S)
    (x y z : S) (hx : x * y = f X) (hy : y * z = 1) :
    eval‚ÇÇ f y (Affine.polynomial W) = y ^ 3 * aevalAEval x z (polynomialInf W) := by
  rw [‚Üê coe_eval‚ÇÇRingHom]
  simp only [polynomialInf, map_add, map_neg, map_pow, coe_eval‚ÇÇRingHom, eval‚ÇÇ_C, RingHom.coe_coe,
    map_mul, eval‚ÇÇ_X, map_sub, map_one, Affine.polynomial, aevalAEval_apply, aeval_C, aeval_X]
  conv in y ^ 2 => rw [‚Üê one_mul (y ^ 2), ‚Üê one_pow 2]
  rw [‚Üê one_mul (f <| C W.a‚ÇÇ)]
  conv in _ * y => rw [‚Üê one_mul y, ‚Üê one_mul (f <| C W.a‚ÇÉ)]
  conv in f (C W.a‚ÇÑ) => rw [‚Üê one_mul (f <| C W.a‚ÇÑ), ‚Üê one_pow 2]
  conv in f (C W.a‚ÇÜ) => rw [‚Üê one_mul (f <| C W.a‚ÇÜ), ‚Üê one_pow 3]
  rw [‚Üê hx, ‚Üê hy]
  simp only [C_eq_algebraMap, AlgHom.commutes]
  ring

/-- The coordinate ring at infinity. -/
abbrev CoordinateRingInf : Type u := AdjoinRoot (polynomialInf W)

/-- -/
abbrev CoordinateRingInf.mk : R[X][Y] ‚Üí+* CoordinateRingInf W := AdjoinRoot.mk _

/-- The coordinate ring localized at Y. -/
abbrev LocalizationY : Type u := Localization.Away (Affine.CoordinateRing.mk W Y)

/-- The coordinate ring at infinity localized at Z. -/
abbrev LocalizationZ : Type u := Localization.Away (CoordinateRingInf.mk W Y)

/-- The R-algebra homomorphism from `CoordinateRingInf W` to `LocalizationY W`. -/
def coordinateRingInfToLoc : CoordinateRingInf W ‚Üí‚Çê[R] LocalizationY W where
  __ := AdjoinRoot.lift
      (aeval <| .mk (.mk W <| Polynomial.C X) ‚ü®_, 1, rfl‚ü©).toRingHom (.mk 1 ‚ü®_, 1, rfl‚ü©) <| by
    let f := IsScalarTower.toAlgHom R (Affine.CoordinateRing W) (LocalizationY W)
    rw [AlgHom.toRingHom_eq_coe, eval‚ÇÇ_polynomialInf _ _ (f <| .mk W <| C X) (f <| .mk W Y)]
    ¬∑ rw [aevalAEval_algHom, AdjoinRoot.aevalAEval_X_Y, map_zero, mul_zero]
    ¬∑ erw [‚Üê Algebra.smul_def, Localization.smul_mk, smul_eq_mul, mul_one, aeval_X]
    ¬∑ erw [‚Üê Algebra.smul_def, Localization.smul_mk, smul_eq_mul, mul_one]
      simp_rw [pow_one]; exact Localization.mk_self ‚ü®_, _‚ü©
  commutes' r := (AdjoinRoot.lift_of _).trans (aeval_C _ _)

/-- The R-algebra homomorphism from `Affine.CoordinateRing W` to `LocalizationZ W`. -/
def coordinateRingToLoc : Affine.CoordinateRing W ‚Üí‚Çê[R] LocalizationZ W where
  __ := AdjoinRoot.lift
      (aeval <| .mk (.mk W <| Polynomial.C X) ‚ü®_, 1, rfl‚ü©).toRingHom (.mk 1 ‚ü®_, 1, rfl‚ü©) <| by
    let f := IsScalarTower.toAlgHom R (CoordinateRingInf W) (LocalizationZ W)
    rw [AlgHom.toRingHom_eq_coe, eval‚ÇÇ_affine_polynomial _ _ (f <| .mk W <| C X) _ (f <| .mk W Y)]
    ¬∑ rw [aevalAEval_algHom, AdjoinRoot.aevalAEval_X_Y, map_zero, mul_zero]
    ¬∑ erw [‚Üê Algebra.smul_def, Localization.smul_mk, smul_eq_mul, mul_one, aeval_X]
    ¬∑ erw [mul_comm, ‚Üê Algebra.smul_def, Localization.smul_mk, smul_eq_mul, mul_one]
      simp_rw [pow_one]; exact Localization.mk_self ‚ü®_, _‚ü©
  commutes' r := (AdjoinRoot.lift_of _).trans (aeval_C _ _)

lemma coordinateRingToLoc_Y : coordinateRingToLoc W (.mk W Y) = .mk 1 ‚ü®_, 1, rfl‚ü© := by
  rw [coordinateRingToLoc, AlgHom.coe_mk]
  exact (AdjoinRoot.lift_mk _ _).trans (eval‚ÇÇ_X _ _)

lemma coordinateRingToLoc_Z : coordinateRingInfToLoc W (.mk W Y) = .mk 1 ‚ü®_, 1, rfl‚ü© := by
  rw [coordinateRingInfToLoc, AlgHom.coe_mk]
  exact (AdjoinRoot.lift_mk _ _).trans (eval‚ÇÇ_X _ _)

def locRingEquiv : LocalizationZ W ‚âÉ+* LocalizationY W :=
  .ofHomInv (Localization.awayLift (coordinateRingInfToLoc W) _ <|
      by erw [coordinateRingToLoc_Z]; apply Localization.isUnit_mk_one)
    (Localization.awayLift (coordinateRingToLoc W) _ <|
      by erw [coordinateRingInfToLoc_Y]; apply Localization.isUnit_mk_one)
    (IsLocalization.ringHom_ext (.powers <| CoordinateRingInf.mk W Y) <| by sorry)
    (IsLocalization.ringHom_ext (.powers <| Affine.CoordinateRing.mk W Y) <| by sorry)
    -- need a version of AdjoinRoot.algHom_ext that takes an R-AlgHom rather than R[X]-AlgHom

instance : Algebra (CoordinateRingInf W) (LocalizationY W) := (coordinateRingInfToLoc W).toAlgebra

def locAlgEquiv : LocalizationZ W ‚âÉ‚Çê[CoordinateRingInf W] LocalizationY W where
  __ := locRingEquiv W
  commutes' := IsLocalization.Away.AwayMap.lift_eq _ _

instance : IsLocalization.Away (CoordinateRingInf.mk W Y) (LocalizationY W) :=
  IsLocalization.isLocalization_of_algEquiv (.powers <| CoordinateRingInf.mk W Y) (locAlgEquiv W)

open CategoryTheory in
lemma _root_.AlgebraicGeometry.IsOpenImmersion.of_isLocalization {R S} [CommRing R] [CommRing S]
    [Algebra R S] (f : R) [IsLocalization.Away f S] :
    IsOpenImmersion (Spec.map (CommRingCat.ofHom (algebraMap R S))) := by
  sorry -- https://github.com/leanprover-community/mathlib4/pull/14428/files#r1666189028

instance : IsOpenImmersion <| Spec.map <| CommRingCat.ofHom <|
    algebraMap (Affine.CoordinateRing W) (LocalizationY W) :=
  .of_isLocalization (Affine.CoordinateRing.mk W Y)

instance : IsOpenImmersion <| Spec.map <| CommRingCat.ofHom <|
    algebraMap (CoordinateRingInf W) (LocalizationY W) :=
  .of_isLocalization (CoordinateRingInf.mk W Y)

/-- Glue data for the projective Weierstrass curve. -/
def glueData : Scheme.GlueData := .mk‚ÇÇ
  (Spec.map <| CommRingCat.ofHom <| algebraMap (Affine.CoordinateRing W) <| LocalizationY W)
  (Spec.map <| CommRingCat.ofHom <| algebraMap (CoordinateRingInf W) <| LocalizationY W)

/-- The projective Weierstrass curve as a scheme. -/
def scheme : Scheme := (glueData W).glued

/-- The open cover by two affine opens of a projective Weierstrass curve. -/
def openCover : (scheme W).OpenCover := (glueData W).openCover

/-- The structure morphism of a projective Weierstrass curve. -/
def schemeToBase : scheme W ‚ü∂ Spec ‚ü®R, inferInstance‚ü© :=
  --(openCover W).glueMorphisms (PBool.rec _ _) _
  sorry -- is it easier to use AlgebraicGeometry.ŒìSpec.adjunction directly?

-- def : Over (Spec ‚ü®R, inferInstance‚ü©) := ...
-- TODO: define the negation morphism on `scheme W` and `smoothLocus W`.

/-- The smooth locus of a projective Weierstrass curve. -/
def smoothLocus : TopologicalSpace.Opens (scheme W) where
  carrier := ((glueData W).Œπ .false).val.base ''
    ((PrimeSpectrum.basicOpen <| .mk W <| Affine.polynomialX W).1 ‚à™
      (PrimeSpectrum.basicOpen <| .mk W <| Affine.polynomialY W).1) ‚à™
    ((glueData W).Œπ .true).val.base '' (PrimeSpectrum.basicOpen <| .mk W <| polynomialZ W).1
  is_open' := by
    apply IsOpen.union <;> apply PresheafedSpace.IsOpenImmersion.base_open.isOpenMap
    on_goal 1 => apply IsOpen.union
    all_goals apply TopologicalSpace.Opens.is_open'

-- TODO: show this scheme is projective (in particular separated) over R,
-- and is integral if R is integral

-- TODO: produce AlgebraicGeometry.Scheme.AffineOpenCover for `scheme W`, `smoothLocus W`
--  and their fiber product with themselves over `R`.

-- define abbrevs X‚ÇÅ, X‚ÇÇ, Y‚ÇÅ, Y‚ÇÇ as elements in `Affine.CoordinateRing W ‚äó[R] Affine.CoordinateRing W` etc.

/- AlgebraicGeometry.Scheme.Pullback.gluing -/

-- TODO: define the notion of a group scheme G over a base scheme S.
-- (Do we want to use Adam Topaz's approach to define it as a "group object" in the category of schemes?)
-- Show that for a scheme T over S, G_S(T) (morphisms from T/S to G/S in the over category) is a group.

-- TODO: Show `smoothLocus W` is a group scheme over `Spec R`.
-- In fact, this extends to a group scheme action of `smoothLocus W` on `scheme W`.
-- Show if T is Spec of a field, we recover `WeierstrassCurve.Affine.Point W` and the group laws agree.

end

end WeierstrassCurve.Projective
