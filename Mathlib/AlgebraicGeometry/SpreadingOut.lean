/-
Copyright (c) 2024 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.FiniteType
import Mathlib.AlgebraicGeometry.Noetherian
import Mathlib.AlgebraicGeometry.Stalk
import Mathlib.AlgebraicGeometry.Properties

/-!
# Spreading out morphisms

Under certain conditions, a morphism on stalks `Spec ğ’ª_{X, x} âŸ¶ Spec ğ’ª_{Y, y}` can be spread
out into a neighborhood of `x`.

## Main result
Given `S`-schemes `X Y` and points `x : X` `y : Y` over `s : S`.
Suppose we have the following diagram of `S`-schemes
```
Spec ğ’ª_{X, x} âŸ¶ X
    |
  Spec(Ï†)
    â†“
Spec ğ’ª_{Y, y} âŸ¶ Y
```
We would like to spread `Spec(Ï†)` out to an `S`-morphism on an open subscheme `U âŠ† X`
```
Spec ğ’ª_{X, x} âŸ¶ U âŠ† X
    |             |
  Spec(Ï†)         |
    â†“             â†“
Spec ğ’ª_{Y, y} âŸ¶ Y
```
- `AlgebraicGeometry.spread_out_unique_of_isGermInjective`:
  The lift is "unique" if the germ map is injective at `x`.
- `AlgebraicGeometry.spread_out_of_isGermInjective`:
  The lift exists if `Y` is locally of finite type and the germ map is injective at `x`.

## TODO

Show that certain morphism properties can also be spread out.

-/

universe u

open CategoryTheory

namespace AlgebraicGeometry

variable {X Y S : Scheme.{u}} (f : X âŸ¶ Y) (sX : X âŸ¶ S) (sY : Y âŸ¶ S) {R A : CommRingCat.{u}}

/-- The germ map at `x` is injective if there exists some affine `U âˆ‹ x`
  such that the map `Î“(X, U) âŸ¶ X_x` is injective -/
class Scheme.IsGermInjectiveAt (X : Scheme.{u}) (x : X) : Prop where
  cond : âˆƒ (U : X.Opens) (hx : x âˆˆ U), IsAffineOpen U âˆ§ Function.Injective (X.presheaf.germ U x hx)

lemma injective_germ_basicOpen (U : X.Opens) (hU : IsAffineOpen U)
    (x : X) (hx : x âˆˆ U) (f : Î“(X, U))
    (hf : x âˆˆ X.basicOpen f)
    (H : Function.Injective (X.presheaf.germ U x hx)) :
    Function.Injective (X.presheaf.germ (X.basicOpen f) x hf) := by
  rw [RingHom.injective_iff_ker_eq_bot, RingHom.ker_eq_bot_iff_eq_zero] at H âŠ¢
  intro t ht
  have := hU.isLocalization_basicOpen f
  obtain âŸ¨t, s, rflâŸ© := IsLocalization.exists_mk'_eq (.powers f) t
  rw [â† RingHom.mem_ker, IsLocalization.mk'_eq_mul_mk'_one, Ideal.mul_unit_mem_iff_mem,
    RingHom.mem_ker, RingHom.algebraMap_toAlgebra, TopCat.Presheaf.germ_res_apply] at ht
  swap; Â· exact @isUnit_of_invertible _ _ _ (@IsLocalization.invertible_mk'_one ..)
  rw [H _ ht, IsLocalization.mk'_zero]

lemma Scheme.exists_germ_injective (X : Scheme.{u}) (x : X) [X.IsGermInjectiveAt x] :
    âˆƒ (U : X.Opens) (hx : x âˆˆ U),
      IsAffineOpen U âˆ§ Function.Injective (X.presheaf.germ U x hx) :=
  Scheme.IsGermInjectiveAt.cond

lemma Scheme.exists_le_and_germ_injective (X : Scheme.{u}) (x : X) [X.IsGermInjectiveAt x]
    (V : X.Opens) (hxV : x âˆˆ V) :
    âˆƒ (U : X.Opens) (hx : x âˆˆ U),
      IsAffineOpen U âˆ§ U â‰¤ V âˆ§ Function.Injective (X.presheaf.germ U x hx) := by
  obtain âŸ¨U, hx, hU, HâŸ© := Scheme.IsGermInjectiveAt.cond (x := x)
  obtain âŸ¨f, hf, hxfâŸ© := hU.exists_basicOpen_le âŸ¨x, hxVâŸ© hx
  exact âŸ¨X.basicOpen f, hxf, hU.basicOpen f, hf, injective_germ_basicOpen U hU x hx f hxf HâŸ©

instance (x : X) [X.IsGermInjectiveAt x] [IsOpenImmersion f] :
    Y.IsGermInjectiveAt (f x) := by
  obtain âŸ¨U, hxU, hU, HâŸ© := X.exists_germ_injective x
  refine âŸ¨âŸ¨f ''áµ U, âŸ¨x, hxU, rflâŸ©, hU.image_of_isOpenImmersion f, ?_âŸ©âŸ©
  refine ((MorphismProperty.injective CommRingCat).cancel_right_of_respectsIso _
    (f.stalkMap x)).mp ?_
  refine ((MorphismProperty.injective CommRingCat).cancel_left_of_respectsIso
    (f.appIso U).inv _).mp ?_
  simpa

variable {f} in
lemma isGermInjectiveAt_iff_of_isOpenImmersion {x : X} [IsOpenImmersion f] :
    Y.IsGermInjectiveAt (f x) â†” X.IsGermInjectiveAt x := by
  refine âŸ¨fun H â†¦ ?_, fun _ â†¦ inferInstanceâŸ©
  obtain âŸ¨U, hxU, hU, hU', HâŸ© :=
    Y.exists_le_and_germ_injective (f x) (V := f.opensRange) âŸ¨x, rflâŸ©
  obtain âŸ¨V, hVâŸ© := (IsOpenImmersion.affineOpensEquiv f).surjective âŸ¨âŸ¨U, hUâŸ©, hU'âŸ©
  obtain rfl : f ''áµ V = U := Subtype.eq_iff.mp (Subtype.eq_iff.mp hV)
  obtain âŸ¨y, hy, e : f y = f xâŸ© := hxU
  obtain rfl := f.isOpenEmbedding.injective e
  refine âŸ¨V, hy, V.2, ?_âŸ©
  replace H := ((MorphismProperty.injective CommRingCat).cancel_right_of_respectsIso _
    (f.stalkMap y)).mpr H
  replace H := ((MorphismProperty.injective CommRingCat).cancel_left_of_respectsIso
    (f.appIso V).inv _).mpr H
  simpa using H

/--
The class of schemes such that for each `x : X`,
`Î“(X, U) âŸ¶ X_x` is injective for some affine `U` containing `x`.

This is typically satisfied when `X` is integral or locally Noetherian.
-/
abbrev Scheme.IsGermInjective (X : Scheme.{u}) := âˆ€ x : X, X.IsGermInjectiveAt x

lemma Scheme.IsGermInjective.of_openCover
    {X : Scheme.{u}} (ğ’° : X.OpenCover) [âˆ€ i, (ğ’°.X i).IsGermInjective] : X.IsGermInjective := by
  intro x
  rw [â† (ğ’°.covers x).choose_spec]
  infer_instance

protected
lemma Scheme.IsGermInjective.Spec
    (H : âˆ€ I : Ideal R, I.IsPrime â†’
      âˆƒ f : R, f âˆ‰ I âˆ§ âˆ€ (x y : R), y * x = 0 â†’ y âˆ‰ I â†’ âˆƒ n, f ^ n * x = 0) :
    (Spec R).IsGermInjective := by
  refine fun p â†¦ âŸ¨?_âŸ©
  obtain âŸ¨f, hf, HâŸ© := H p.asIdeal p.2
  refine âŸ¨PrimeSpectrum.basicOpen f, hf, ?_, ?_âŸ©
  Â· rw [â† basicOpen_eq_of_affine]
    exact (isAffineOpen_top (Spec R)).basicOpen _
  rw [RingHom.injective_iff_ker_eq_bot, RingHom.ker_eq_bot_iff_eq_zero]
  intro x hx
  obtain âŸ¨x, s, rflâŸ© := IsLocalization.exists_mk'_eq
    (S := ((Spec.structureSheaf R).val.obj (.op <| PrimeSpectrum.basicOpen f))) (.powers f) x
  rw [â† RingHom.mem_ker, IsLocalization.mk'_eq_mul_mk'_one, Ideal.mul_unit_mem_iff_mem,
    RingHom.mem_ker, RingHom.algebraMap_toAlgebra] at hx
  swap; Â· exact @isUnit_of_invertible _ _ _ (@IsLocalization.invertible_mk'_one ..)
  -- There is an `Opposite.unop (Opposite.op _)` in `hx` which doesn't seem removable using
  -- `simp`/`rw`.
  erw [StructureSheaf.germ_toOpen] at hx
  obtain âŸ¨âŸ¨y, hyâŸ©, hy'âŸ© := (IsLocalization.map_eq_zero_iff p.asIdeal.primeCompl
    ((Spec.structureSheaf R).presheaf.stalk p) _).mp hx
  obtain âŸ¨n, hnâŸ© := H x y hy' hy
  refine (@IsLocalization.mk'_eq_zero_iff ..).mpr ?_
  exact âŸ¨âŸ¨_, n, rflâŸ©, hnâŸ©

instance (priority := 100) [IsIntegral X] : X.IsGermInjective := by
  refine fun x â†¦ âŸ¨âŸ¨(X.affineCover.f _).opensRange, X.affineCover.covers x,
    (isAffineOpen_opensRange (X.affineCover.f _)), ?_âŸ©âŸ©
  have : Nonempty (X.affineCover.f _).opensRange := âŸ¨âŸ¨_, X.affineCover.covers xâŸ©âŸ©
  have := (isAffineOpen_opensRange (X.affineCover.f _)).isLocalization_stalk
    âŸ¨_, X.affineCover.covers xâŸ©
  exact @IsLocalization.injective _ _ _ _ _ (show _ from _) this
    (Ideal.primeCompl_le_nonZeroDivisors _)

instance (priority := 100) [IsLocallyNoetherian X] : X.IsGermInjective := by
  suffices âˆ€ (R : CommRingCat.{u}) (_ : IsNoetherianRing R), (Spec R).IsGermInjective by
    refine @Scheme.IsGermInjective.of_openCover _ (X.affineOpenCover.openCover) (fun i â†¦ this _ ?_)
    have := isLocallyNoetherian_of_isOpenImmersion (X.affineOpenCover.f i)
    infer_instance
  refine fun R hR â†¦ Scheme.IsGermInjective.Spec fun I hI â†¦ ?_
  let J := RingHom.ker <| algebraMap R (Localization.AtPrime I)
  have hJ (x) : x âˆˆ J â†” âˆƒ y : I.primeCompl, y * x = 0 :=
    IsLocalization.map_eq_zero_iff I.primeCompl _ x
  choose f hf using fun x â†¦ (hJ x).mp
  obtain âŸ¨s, hsâŸ© := (isNoetherianRing_iff_ideal_fg R).mp â€¹_â€º J
  have hs' : (s : Set R) âŠ† J := hs â–¸ Ideal.subset_span
  refine âŸ¨_, (s.attach.prod fun x â†¦ f x (hs' x.2)).2, fun x y e hy â†¦ âŸ¨1, ?_âŸ©âŸ©
  rw [pow_one, mul_comm, â† smul_eq_mul, â† Submodule.mem_annihilator_span_singleton]
  refine SetLike.le_def.mp ?_ ((hJ x).mpr âŸ¨âŸ¨y, hyâŸ©, eâŸ©)
  rw [â† hs, Ideal.span_le]
  intro i hi
  rw [SetLike.mem_coe, Submodule.mem_annihilator_span_singleton, smul_eq_mul,
    mul_comm, â† smul_eq_mul, â† Submodule.mem_annihilator_span_singleton, Submonoid.coe_finset_prod]
  refine Ideal.mem_of_dvd _ (Finset.dvd_prod_of_mem _ (s.mem_attach âŸ¨i, hiâŸ©)) ?_
  rw [Submodule.mem_annihilator_span_singleton, smul_eq_mul]
  exact hf i _

/--
Let `x : X` and `f g : X âŸ¶ Y` be two morphisms such that `f x = g x`.
If `f` and `g` agree on the stalk of `x`, then they agree on an open neighborhood of `x`,
provided `X` is "germ-injective" at `x` (e.g. when it's integral or locally Noetherian).

TODO: The condition on `X` is unnecessary when `Y` is locally of finite type.
-/
@[stacks 0BX6]
lemma spread_out_unique_of_isGermInjective {x : X} [X.IsGermInjectiveAt x]
    (f g : X âŸ¶ Y) (e : f x = g x)
    (H : f.stalkMap x =
      Y.presheaf.stalkSpecializes (Inseparable.of_eq e.symm).specializes â‰« g.stalkMap x) :
    âˆƒ (U : X.Opens), x âˆˆ U âˆ§ U.Î¹ â‰« f = U.Î¹ â‰« g := by
  obtain âŸ¨_, âŸ¨V : Y.Opens, hV, rflâŸ©, hxV, -âŸ© :=
    Y.isBasis_affineOpens.exists_subset_of_mem_open (Set.mem_univ (f x)) isOpen_univ
  have hxV' : g x âˆˆ V := e â–¸ hxV
  obtain âŸ¨U, hxU, _, hUV, HUâŸ© := X.exists_le_and_germ_injective x (f â»Â¹áµ V âŠ“ g â»Â¹áµ V) âŸ¨hxV, hxV'âŸ©
  refine âŸ¨U, hxU, ?_âŸ©
  rw [â† Scheme.Hom.resLE_comp_Î¹ _ (hUV.trans inf_le_left),
    â† Scheme.Hom.resLE_comp_Î¹ _ (hUV.trans inf_le_right)]
  congr 1
  have : IsAffine V := hV
  suffices âˆ€ (Uâ‚€ Vâ‚€) (eU : U = Uâ‚€) (eV : V = Vâ‚€),
      f.appLE Vâ‚€ Uâ‚€ (eU â–¸ eV â–¸ hUV.trans inf_le_left) =
        g.appLE Vâ‚€ Uâ‚€ (eU â–¸ eV â–¸ hUV.trans inf_le_right) by
    rw [â† cancel_mono V.toScheme.isoSpec.hom]
    simp only [Scheme.isoSpec, asIso_hom, Scheme.toSpecÎ“_naturality,
      Scheme.Hom.app_eq_appLE, Scheme.Hom.resLE_appLE]
    congr 2
    apply this <;> simp
  rintro U V rfl rfl
  have := ConcreteCategory.mono_of_injective _ HU
  rw [â† cancel_mono (X.presheaf.germ U x hxU)]
  simp only [Scheme.Hom.appLE, Category.assoc, X.presheaf.germ_res', â† Scheme.Hom.germ_stalkMap, H]
  simp only [TopCat.Presheaf.germ_stalkSpecializes_assoc, Scheme.Hom.germ_stalkMap]

/--
A variant of `spread_out_unique_of_isGermInjective`
whose condition is an equality of scheme morphisms instead of ring homomorphisms.
-/
lemma spread_out_unique_of_isGermInjective' {x : X} [X.IsGermInjectiveAt x]
    (f g : X âŸ¶ Y)
    (e : X.fromSpecStalk x â‰« f = X.fromSpecStalk x â‰« g) :
    âˆƒ (U : X.Opens), x âˆˆ U âˆ§ U.Î¹ â‰« f = U.Î¹ â‰« g := by
  fapply spread_out_unique_of_isGermInjective
  Â· simpa using congr($e (IsLocalRing.closedPoint _))
  Â· apply Spec.map_injective
    rw [â† cancel_mono (Y.fromSpecStalk _)]
    simpa [Scheme.SpecMap_stalkSpecializes_fromSpecStalk]

lemma exists_lift_of_germInjective_aux {U : X.Opens} {x : X} (hxU)
    (Ï† : A âŸ¶ X.presheaf.stalk x) (Ï†RA : R âŸ¶ A) (Ï†RX : R âŸ¶ Î“(X, U))
    (hÏ†RA : RingHom.FiniteType Ï†RA.hom)
    (e : Ï†RA â‰« Ï† = Ï†RX â‰« X.presheaf.germ U x hxU) :
    âˆƒ (V : X.Opens) (hxV : x âˆˆ V),
      V â‰¤ U âˆ§ RingHom.range Ï†.hom â‰¤ RingHom.range (X.presheaf.germ V x hxV).hom := by
  letI := Ï†RA.hom.toAlgebra
  obtain âŸ¨s, hsâŸ© := hÏ†RA
  choose W hxW f hf using fun t â†¦ X.presheaf.germ_exist x (Ï† t)
  have H : x âˆˆ s.inf W âŠ“ U := by
    rw [â† SetLike.mem_coe, TopologicalSpace.Opens.coe_inf, TopologicalSpace.Opens.coe_finset_inf]
    exact âŸ¨by simpa using fun x _ â†¦ hxW x, hxUâŸ©
  refine âŸ¨s.inf W âŠ“ U, H, inf_le_right, ?_âŸ©
  letI := Ï†RX.hom.toAlgebra
  letI := (Ï†RX â‰« X.presheaf.germ U x hxU).hom.toAlgebra
  letI := (Ï†RX â‰« X.presheaf.map (homOfLE (inf_le_right (a := s.inf W))).op).hom.toAlgebra
  let Ï†' : A â†’â‚[R] X.presheaf.stalk x :=
    { Ï†.hom with commutes' := DFunLike.congr_fun (congr_arg CommRingCat.Hom.hom e) }
  let Ïˆ : Î“(X, s.inf W âŠ“ U) â†’â‚[R] X.presheaf.stalk x :=
    { (X.presheaf.germ _ x H).hom with commutes' := fun x â†¦ X.presheaf.germ_res_apply _ _ _ _ }
  change AlgHom.range Ï†' â‰¤ AlgHom.range Ïˆ
  rw [â† Algebra.map_top, â† hs, AlgHom.map_adjoin, Algebra.adjoin_le_iff]
  rintro _ âŸ¨i, hi, rfl : Ï† i = _âŸ©
  refine âŸ¨X.presheaf.map (homOfLE (inf_le_left.trans (Finset.inf_le hi))).op (f i), ?_âŸ©
  exact (X.presheaf.germ_res_apply _ _ _ _).trans (hf _)

/--
Suppose `X` is a scheme, `x : X` such that the germ map at `x` is (locally) injective,
and `U` is a neighborhood of `x`.
Given a commutative diagram of `CommRingCat`
```
R âŸ¶ Î“(X, U)
â†“    â†“
A âŸ¶ ğ’ª_{X, x}
```
such that `R` is of finite type over `A`, we may lift `A âŸ¶ ğ’ª_{X, x}` to some `A âŸ¶ Î“(X, V)`.
-/
lemma exists_lift_of_germInjective {x : X} [X.IsGermInjectiveAt x] {U : X.Opens} (hxU : x âˆˆ U)
    (Ï† : A âŸ¶ X.presheaf.stalk x) (Ï†RA : R âŸ¶ A) (Ï†RX : R âŸ¶ Î“(X, U))
    (hÏ†RA : RingHom.FiniteType Ï†RA.hom)
    (e : Ï†RA â‰« Ï† = Ï†RX â‰« X.presheaf.germ U x hxU) :
    âˆƒ (V : X.Opens) (hxV : x âˆˆ V) (Ï†' : A âŸ¶ Î“(X, V)) (i : V â‰¤ U), IsAffineOpen V âˆ§
      Ï† = Ï†' â‰« X.presheaf.germ V x hxV âˆ§ Ï†RX â‰« X.presheaf.map i.hom.op = Ï†RA â‰« Ï†' := by
  obtain âŸ¨V, hxV, iVU, hVâŸ© := exists_lift_of_germInjective_aux hxU Ï† Ï†RA Ï†RX hÏ†RA e
  obtain âŸ¨V', hxV', hV', iV'V, HâŸ© := X.exists_le_and_germ_injective x V hxV
  let f := X.presheaf.germ V' x hxV'
  have hf' : RingHom.range (X.presheaf.germ V x hxV).hom â‰¤ RingHom.range f.hom := by
    rw [â† X.presheaf.germ_res iV'V.hom _ hxV']
    exact Set.range_comp_subset_range (X.presheaf.map iV'V.hom.op) f
  let e := RingEquiv.ofLeftInverse H.hasLeftInverse.choose_spec
  refine âŸ¨V', hxV', CommRingCat.ofHom (e.symm.toRingHom.comp
    (Ï†.hom.codRestrict _ (fun x â†¦ hf' (hV âŸ¨x, rflâŸ©)))), iV'V.trans iVU, hV', ?_, ?_âŸ©
  Â· ext a
    change Ï† a = (e (e.symm _)).1
    simp only [RingEquiv.apply_symm_apply]
    rfl
  Â· ext a
    apply e.injective
    change e _ = e (e.symm _)
    rw [RingEquiv.apply_symm_apply]
    ext
    change X.presheaf.germ _ _ _ (X.presheaf.map _ _) = (Ï†RA â‰« Ï†) a
    rw [TopCat.Presheaf.germ_res_apply, â€¹Ï†RA â‰« Ï† = _â€º]
    rfl

/--
Given `S`-schemes `X Y` and points `x : X` `y : Y` over `s : S`.
Suppose we have the following diagram of `S`-schemes
```
Spec ğ’ª_{X, x} âŸ¶ X
    |
  Spec(Ï†)
    â†“
Spec ğ’ª_{Y, y} âŸ¶ Y
```
Then the map `Spec(Ï†)` spreads out to an `S`-morphism on an open subscheme `U âŠ† X`,
```
Spec ğ’ª_{X, x} âŸ¶ U âŠ† X
    |             |
  Spec(Ï†)         |
    â†“             â†“
Spec ğ’ª_{Y, y} âŸ¶ Y
```
provided that `Y` is locally of finite type over `S` and
`X` is "germ-injective" at `x` (e.g. when it's integral or locally Noetherian).

TODO: The condition on `X` is unnecessary when `Y` is locally of finite presentation.
-/
@[stacks 0BX6]
lemma spread_out_of_isGermInjective [LocallyOfFiniteType sY] {x : X} [X.IsGermInjectiveAt x] {y : Y}
    (e : sX x = sY y) (Ï† : Y.presheaf.stalk y âŸ¶ X.presheaf.stalk x)
    (h : sY.stalkMap y â‰« Ï† =
      S.presheaf.stalkSpecializes (Inseparable.of_eq e).specializes â‰« sX.stalkMap x) :
    âˆƒ (U : X.Opens) (hxU : x âˆˆ U) (f : U.toScheme âŸ¶ Y),
      Spec.map Ï† â‰« Y.fromSpecStalk y = U.fromSpecStalkOfMem x hxU â‰« f âˆ§
      f â‰« sY = U.Î¹ â‰« sX := by
  obtain âŸ¨_, âŸ¨U, hU, rflâŸ©, hxU, -âŸ© :=
    S.isBasis_affineOpens.exists_subset_of_mem_open (Set.mem_univ (sX x)) isOpen_univ
  have hyU : sY y âˆˆ U := e â–¸ hxU
  obtain âŸ¨_, âŸ¨V : Y.Opens, hV, rflâŸ©, hyV, iVUâŸ© :=
    Y.isBasis_affineOpens.exists_subset_of_mem_open hyU (sY â»Â¹áµ U).2
  have : sY.appLE U V iVU â‰« Y.presheaf.germ V y hyV â‰« Ï† =
      sX.app U â‰« X.presheaf.germ (sX â»Â¹áµ U) x hxU := by
    rw [Scheme.Hom.appLE, Category.assoc, Y.presheaf.germ_res_assoc,
      â† Scheme.Hom.germ_stalkMap_assoc, h]
    simp
  obtain âŸ¨W, hxW, Ï†', i, hW, hâ‚, hâ‚‚âŸ© :=
    exists_lift_of_germInjective (R := Î“(S, U)) (A := Î“(Y, V)) (U := sX â»Â¹áµ U) (x := x) hxU
    (Y.presheaf.germ _ y hyV â‰« Ï†) (sY.appLE U V iVU) (sX.app U)
    (LocallyOfFiniteType.finiteType_of_affine_subset âŸ¨_, hUâŸ© âŸ¨_, hVâŸ© _) this
  refine âŸ¨W, hxW, W.toSpecÎ“ â‰« Spec.map Ï†' â‰« hV.fromSpec, ?_, ?_âŸ©
  Â· rw [W.fromSpecStalkOfMem_toSpecÎ“_assoc x hxW, â† Spec.map_comp_assoc, â† hâ‚,
      Spec.map_comp, Category.assoc, â† IsAffineOpen.fromSpecStalk,
      IsAffineOpen.fromSpecStalk_eq_fromSpecStalk]
  Â· simp only [Category.assoc]
    rw [â† IsAffineOpen.SpecMap_appLE_fromSpec sY hU hV iVU, â† Spec.map_comp_assoc, â† hâ‚‚,
      â† Scheme.Hom.appLE, â† hW.isoSpec_hom, IsAffineOpen.SpecMap_appLE_fromSpec sX hU hW i,
      â† Iso.eq_inv_comp, IsAffineOpen.isoSpec_inv_Î¹_assoc]

/--
Given `S`-schemes `X Y`, a point `x : X`, and a `S`-morphism `Ï† : Spec ğ’ª_{X, x} âŸ¶ Y`,
we may spread it out to an `S`-morphism `f : U âŸ¶ Y`
provided that `Y` is locally of finite type over `S` and
`X` is "germ-injective" at `x` (e.g. when it's integral or locally Noetherian).

TODO: The condition on `X` is unnecessary when `Y` is locally of finite presentation.
-/
lemma spread_out_of_isGermInjective' [LocallyOfFiniteType sY] {x : X} [X.IsGermInjectiveAt x]
    (Ï† : Spec (X.presheaf.stalk x) âŸ¶ Y)
    (h : Ï† â‰« sY = X.fromSpecStalk x â‰« sX) :
    âˆƒ (U : X.Opens) (hxU : x âˆˆ U) (f : U.toScheme âŸ¶ Y),
      Ï† = U.fromSpecStalkOfMem x hxU â‰« f âˆ§ f â‰« sY = U.Î¹ â‰« sX := by
  have := spread_out_of_isGermInjective sX sY ?_ (Scheme.stalkClosedPointTo Ï†) ?_
  Â· simpa only [Scheme.Spec_stalkClosedPointTo_fromSpecStalk] using this
  Â· rw [â† Scheme.Hom.comp_apply, h, Scheme.Hom.comp_apply, Scheme.fromSpecStalk_closedPoint]
  Â· apply Spec.map_injective
    rw [â† cancel_mono (S.fromSpecStalk _)]
    simpa only [Spec.map_comp, Category.assoc, Scheme.SpecMap_stalkMap_fromSpecStalk,
      Scheme.Spec_stalkClosedPointTo_fromSpecStalk_assoc,
      Scheme.SpecMap_stalkSpecializes_fromSpecStalk]

end AlgebraicGeometry
