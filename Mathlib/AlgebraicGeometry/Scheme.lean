/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.AlgebraicGeometry.Spec
import Mathlib.Algebra.Category.Ring.Constructions
import Mathlib.CategoryTheory.Elementwise

/-!
# The category of schemes

A scheme is a locally ringed space such that every point is contained in some open set
where there is an isomorphism of presheaves between the restriction to that open set,
and the structure sheaf of `Spec R`, for some commutative ring `R`.

A morphism of schemes is just a morphism of the underlying locally ringed spaces.

## Notation

`Spec R` typechecks only for `R : CommRingCat`. It happens quite often that we want to take Spec of
an unbundled ring, and this can be spelled `Spec (CommRingCat.of R)`, or `Spec (.of R)` using
anonymous dot notation. This is such a common situation that we have dedicated notation: `Spec(R)`

Note that one can write `Spec(R)` for `R : CommRingCat`, but one shouldn't: This is `Spec (.of â†‘R)`
under the hood, which simplifies to `Spec R`.
-/

-- Explicit universe annotations were used in this file to improve performance https://github.com/leanprover-community/mathlib4/issues/12737


universe u

noncomputable section

open TopologicalSpace

open CategoryTheory

open TopCat

open Opposite

namespace AlgebraicGeometry

/-- We define `Scheme` as an `X : LocallyRingedSpace`,
along with a proof that every point has an open neighbourhood `U`
so that the restriction of `X` to `U` is isomorphic,
as a locally ringed space, to `Spec.toLocallyRingedSpace.obj (op R)`
for some `R : CommRingCat`.
-/
structure Scheme extends LocallyRingedSpace where
  local_affine :
    âˆ€ x : toLocallyRingedSpace,
      âˆƒ (U : OpenNhds x) (R : CommRingCat),
        Nonempty
          (toLocallyRingedSpace.restrict U.isOpenEmbedding â‰… Spec.toLocallyRingedSpace.obj (op R))

namespace Scheme

instance : CoeSort Scheme Type* where
  coe X := X.carrier

open Lean PrettyPrinter.Delaborator SubExpr in
/-- Pretty printer for coercing schemes to types. -/
@[app_delab TopCat.carrier]
partial def delabAdjoinNotation : Delab := whenPPOption getPPNotation do
  guard <| (â† getExpr).isAppOfArity ``TopCat.carrier 1
  withNaryArg 0 do
  guard <| (â† getExpr).isAppOfArity ``PresheafedSpace.carrier 3
  withNaryArg 2 do
  guard <| (â† getExpr).isAppOfArity ``SheafedSpace.toPresheafedSpace 3
  withNaryArg 2 do
  guard <| (â† getExpr).isAppOfArity ``LocallyRingedSpace.toSheafedSpace 1
  withNaryArg 0 do
  guard <| (â† getExpr).isAppOfArity ``Scheme.toLocallyRingedSpace 1
  withNaryArg 0 do
  `(â†¥$(â† delab))

/-- The type of open sets of a scheme. -/
abbrev Opens (X : Scheme) : Type* := TopologicalSpace.Opens X

/-- A morphism between schemes is a morphism between the underlying locally ringed spaces. -/
structure Hom (X Y : Scheme)
  extends toLRSHom' : X.toLocallyRingedSpace.Hom Y.toLocallyRingedSpace where

/-- Cast a morphism of schemes into morphisms of local ringed spaces. -/
abbrev Hom.toLRSHom {X Y : Scheme.{u}} (f : X.Hom Y) :
    X.toLocallyRingedSpace âŸ¶ Y.toLocallyRingedSpace :=
  f.toLRSHom'

/-- See Note [custom simps projection] -/
def Hom.Simps.toLRSHom {X Y : Scheme.{u}} (f : X.Hom Y) :
    X.toLocallyRingedSpace âŸ¶ Y.toLocallyRingedSpace :=
  f.toLRSHom

initialize_simps_projections Hom (toLRSHom' â†’ toLRSHom)

/-- Schemes are a full subcategory of locally ringed spaces.
-/
instance : Category Scheme where
  Hom := Hom
  id X := Hom.mk (ğŸ™ X.toLocallyRingedSpace)
  comp f g := Hom.mk (f.toLRSHom â‰« g.toLRSHom)

/-- `f â»Â¹áµ U` is notation for `(Opens.map f.base).obj U`,
  the preimage of an open set `U` under `f`. -/
scoped[AlgebraicGeometry] notation3:90 f:91 " â»Â¹áµ " U:90 =>
  @Functor.obj (Scheme.Opens _) _ (Scheme.Opens _) _
    (Opens.map (f : Scheme.Hom _ _).base) U

/-- `Î“(X, U)` is notation for `X.presheaf.obj (op U)`. -/
scoped[AlgebraicGeometry] notation3 "Î“(" X ", " U ")" =>
  (PresheafedSpace.presheaf (SheafedSpace.toPresheafedSpace
    (LocallyRingedSpace.toSheafedSpace (Scheme.toLocallyRingedSpace X)))).obj
    (op (Î± := Scheme.Opens _) U)

instance {X : Scheme.{u}} : Subsingleton Î“(X, âŠ¥) :=
  CommRingCat.subsingleton_of_isTerminal X.sheaf.isTerminalOfEmpty

@[continuity, fun_prop]
lemma Hom.continuous {X Y : Scheme} (f : X.Hom Y) : Continuous f.base := f.base.hom.2

/-- The structure sheaf of a scheme. -/
protected abbrev sheaf (X : Scheme) :=
  X.toSheafedSpace.sheaf

/--
We give schemes the specialization preorder by default.
-/
instance {X : Scheme.{u}} : Preorder X := specializationPreorder X

lemma le_iff_specializes {X : Scheme.{u}} {a b : X} : a â‰¤ b â†” b â¤³ a := by rfl

open Order in
lemma height_of_isClosed {X : Scheme} {x : X} (hx : IsClosed {x}) : height x = 0 := by
  simp only [height_eq_zero]
  intro b _
  obtain rfl | h := eq_or_ne b x
  Â· assumption
  Â· have := IsClosed.not_specializes hx rfl h
    contradiction

namespace Hom

variable {X Y : Scheme.{u}} (f : Hom X Y) {U U' : Y.Opens} {V V' : X.Opens}

/-- Given a morphism of schemes `f : X âŸ¶ Y`, and open `U âŠ† Y`,
this is the induced map `Î“(Y, U) âŸ¶ Î“(X, f â»Â¹áµ U)`. -/
abbrev app (U : Y.Opens) : Î“(Y, U) âŸ¶ Î“(X, f â»Â¹áµ U) :=
  f.c.app (op U)

/-- Given a morphism of schemes `f : X âŸ¶ Y`,
this is the induced map `Î“(Y, âŠ¤) âŸ¶ Î“(X, âŠ¤)`. -/
abbrev appTop : Î“(Y, âŠ¤) âŸ¶ Î“(X, âŠ¤) :=
  f.app âŠ¤

@[reassoc]
lemma naturality (i : op U' âŸ¶ op U) :
    Y.presheaf.map i â‰« f.app U = f.app U' â‰« X.presheaf.map ((Opens.map f.base).map i.unop).op :=
  f.c.naturality i

/-- Given a morphism of schemes `f : X âŸ¶ Y`, and open sets `U âŠ† Y`, `V âŠ† f â»Â¹' U`,
this is the induced map `Î“(Y, U) âŸ¶ Î“(X, V)`. -/
def appLE (U : Y.Opens) (V : X.Opens) (e : V â‰¤ f â»Â¹áµ U) : Î“(Y, U) âŸ¶ Î“(X, V) :=
  f.app U â‰« X.presheaf.map (homOfLE e).op

@[reassoc (attr := simp)]
lemma appLE_map (e : V â‰¤ f â»Â¹áµ U) (i : op V âŸ¶ op V') :
    f.appLE U V e â‰« X.presheaf.map i = f.appLE U V' (i.unop.le.trans e) := by
  rw [Hom.appLE, Category.assoc, â† Functor.map_comp]
  rfl

@[reassoc]
lemma appLE_map' (e : V â‰¤ f â»Â¹áµ U) (i : V = V') :
    f.appLE U V' (i â–¸ e) â‰« X.presheaf.map (eqToHom i).op = f.appLE U V e :=
  appLE_map _ _ _

@[reassoc (attr := simp)]
lemma map_appLE (e : V â‰¤ f â»Â¹áµ U) (i : op U' âŸ¶ op U) :
    Y.presheaf.map i â‰« f.appLE U V e =
      f.appLE U' V (e.trans ((Opens.map f.base).map i.unop).le) := by
  rw [Hom.appLE, f.naturality_assoc, â† Functor.map_comp]
  rfl

@[reassoc]
lemma map_appLE' (e : V â‰¤ f â»Â¹áµ U) (i : U' = U) :
    Y.presheaf.map (eqToHom i).op â‰« f.appLE U' V (i â–¸ e) = f.appLE U V e :=
  map_appLE _ _ _

lemma app_eq_appLE {U : Y.Opens} :
    f.app U = f.appLE U _ le_rfl := by
  simp [Hom.appLE]

lemma appLE_eq_app {U : Y.Opens} :
    f.appLE U (f â»Â¹áµ U) le_rfl = f.app U :=
  (app_eq_appLE f).symm

lemma appLE_congr (e : V â‰¤ f â»Â¹áµ U) (eâ‚ : U = U') (eâ‚‚ : V = V')
    (P : âˆ€ {R S : CommRingCat.{u}} (_ : R âŸ¶ S), Prop) :
    P (f.appLE U V e) â†” P (f.appLE U' V' (eâ‚ â–¸ eâ‚‚ â–¸ e)) := by
  subst eâ‚; subst eâ‚‚; rfl

/-- A morphism of schemes `f : X âŸ¶ Y` induces a local ring homomorphism from
`Y.presheaf.stalk (f x)` to `X.presheaf.stalk x` for any `x : X`. -/
def stalkMap (x : X) : Y.presheaf.stalk (f.base x) âŸ¶ X.presheaf.stalk x :=
  f.toLRSHom.stalkMap x

protected lemma ext {f g : X âŸ¶ Y} (h_base : f.base = g.base)
    (h_app : âˆ€ U, f.app U â‰« X.presheaf.map
      (eqToHom congr((Opens.map $h_base.symm).obj U)).op = g.app U) : f = g := by
  cases f; cases g; congr 1
  exact LocallyRingedSpace.Hom.ext' <| SheafedSpace.ext _ _ h_base
    (TopCat.Presheaf.ext fun U â†¦ by simpa using h_app U)

/-- An alternative ext lemma for scheme morphisms. -/
protected lemma ext' {f g : X âŸ¶ Y} (h : f.toLRSHom = g.toLRSHom) : f = g := by
  cases f; cases g; congr 1

lemma preimage_iSup {Î¹} (U : Î¹ â†’ Opens Y) : f â»Â¹áµ iSup U = â¨† i, f â»Â¹áµ U i :=
  Opens.ext (by simp)

lemma preimage_iSup_eq_top {Î¹} {U : Î¹ â†’ Opens Y} (hU : iSup U = âŠ¤) :
    â¨† i, f â»Â¹áµ U i = âŠ¤ := f.preimage_iSup U â–¸ hU â–¸ rfl

lemma preimage_le_preimage_of_le {U U' : Y.Opens} (hUU' : U â‰¤ U') :
    f â»Â¹áµ U â‰¤ f â»Â¹áµ U' :=
  fun _ ha â†¦ hUU' ha

end Hom

@[simp]
lemma preimage_comp {X Y Z : Scheme.{u}} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (U) :
    (f â‰« g) â»Â¹áµ U = f â»Â¹áµ g â»Â¹áµ U := rfl

/-- The forgetful functor from `Scheme` to `LocallyRingedSpace`. -/
@[simps!]
def forgetToLocallyRingedSpace : Scheme â¥¤ LocallyRingedSpace where
  obj := toLocallyRingedSpace
  map := Hom.toLRSHom

/-- The forget functor `Scheme â¥¤ LocallyRingedSpace` is fully faithful. -/
@[simps preimage_toLRSHom]
def fullyFaithfulForgetToLocallyRingedSpace :
    forgetToLocallyRingedSpace.FullyFaithful where
  preimage := Hom.mk

instance : forgetToLocallyRingedSpace.Full :=
  fullyFaithfulForgetToLocallyRingedSpace.full

instance : forgetToLocallyRingedSpace.Faithful :=
  fullyFaithfulForgetToLocallyRingedSpace.faithful

/-- The forgetful functor from `Scheme` to `TopCat`. -/
@[simps!]
def forgetToTop : Scheme â¥¤ TopCat :=
  Scheme.forgetToLocallyRingedSpace â‹™ LocallyRingedSpace.forgetToTop

/-- An isomorphism of schemes induces a homeomorphism of the underlying topological spaces. -/
noncomputable def homeoOfIso {X Y : Scheme.{u}} (e : X â‰… Y) : X â‰ƒâ‚œ Y :=
  TopCat.homeoOfIso (forgetToTop.mapIso e)

@[simp]
lemma coe_homeoOfIso {X Y : Scheme.{u}} (e : X â‰… Y) :
    â‡‘(homeoOfIso e) = e.hom.base := rfl

@[simp]
lemma coe_homeoOfIso_symm {X Y : Scheme.{u}} (e : X â‰… Y) :
    â‡‘(homeoOfIso e.symm) = e.inv.base := rfl

@[simp]
lemma homeoOfIso_symm {X Y : Scheme} (e : X â‰… Y) :
    (homeoOfIso e).symm = homeoOfIso e.symm := rfl

lemma homeoOfIso_apply {X Y : Scheme} (e : X â‰… Y) (x : X) :
    homeoOfIso e x = e.hom.base x := rfl

alias _root_.CategoryTheory.Iso.schemeIsoToHomeo := homeoOfIso

/-- An isomorphism of schemes induces a homeomorphism of the underlying topological spaces. -/
noncomputable def Hom.homeomorph {X Y : Scheme.{u}} (f : X.Hom Y) [IsIso (C := Scheme) f] :
    X â‰ƒâ‚œ Y :=
  (asIso f).schemeIsoToHomeo

@[simp]
lemma Hom.homeomorph_apply {X Y : Scheme.{u}} (f : X.Hom Y) [IsIso (C := Scheme) f] (x) :
    f.homeomorph x = f.base x := rfl

instance hasCoeToTopCat : CoeOut Scheme TopCat where
  coe X := X.carrier

/-- forgetful functor to `TopCat` is the same as coercion -/
unif_hint forgetToTop_obj_eq_coe (X : Scheme) where âŠ¢
  forgetToTop.obj X â‰Ÿ (X : TopCat)

/-- The forgetful functor from `Scheme` to `Type`. -/
nonrec def forget : Scheme.{u} â¥¤ Type u := Scheme.forgetToTop â‹™ forget TopCat

/-- forgetful functor to `Scheme` is the same as coercion -/
-- Schemes are often coerced as types, and it would be useful to have definitionally equal types
-- to be reducibly equal. The alternative is to make `forget` reducible but that option has
-- poor performance consequences.
unif_hint forget_obj_eq_coe (X : Scheme) where âŠ¢
  forget.obj X â‰Ÿ (X : Type*)

@[simp] lemma forget_obj (X) : Scheme.forget.obj X = X := rfl
@[simp] lemma forget_map {X Y} (f : X âŸ¶ Y) : forget.map f = (f.base : X â†’ Y) := rfl

@[simp]
theorem id.base (X : Scheme) : (ğŸ™ X :).base = ğŸ™ _ :=
  rfl

@[simp]
theorem id_app {X : Scheme} (U : X.Opens) :
    (ğŸ™ X :).app U = ğŸ™ _ := rfl

@[simp]
theorem id_appTop {X : Scheme} :
    (ğŸ™ X :).appTop = ğŸ™ _ :=
  rfl

@[reassoc]
theorem comp_toLRSHom {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    (f â‰« g).toLRSHom = f.toLRSHom â‰« g.toLRSHom :=
  rfl

@[simp, reassoc] -- reassoc lemma does not need `simp`
theorem comp_coeBase {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    (f â‰« g).base = f.base â‰« g.base :=
  rfl

@[reassoc]
theorem comp_base {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    (f â‰« g).base = f.base â‰« g.base :=
  rfl

theorem comp_base_apply {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (x : X) :
    (f â‰« g).base x = g.base (f.base x) := by
  simp

@[simp, reassoc] -- reassoc lemma does not need `simp`
theorem comp_app {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (U) :
    (f â‰« g).app U = g.app U â‰« f.app _ :=
  rfl

@[simp, reassoc] -- reassoc lemma does not need `simp`
theorem comp_appTop {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    (f â‰« g).appTop = g.appTop â‰« f.appTop :=
  rfl

theorem appLE_comp_appLE {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (U V W eâ‚ eâ‚‚) :
    g.appLE U V eâ‚ â‰« f.appLE V W eâ‚‚ =
      (f â‰« g).appLE U W (eâ‚‚.trans ((Opens.map f.base).map (homOfLE eâ‚)).le) := by
  dsimp [Hom.appLE]
  rw [Category.assoc, f.naturality_assoc, â† Functor.map_comp]
  rfl

@[simp, reassoc] -- reassoc lemma does not need `simp`
theorem comp_appLE {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (U V e) :
    (f â‰« g).appLE U V e = g.app U â‰« f.appLE _ V e := by
  rw [g.app_eq_appLE, appLE_comp_appLE]

theorem congr_app {X Y : Scheme} {f g : X âŸ¶ Y} (e : f = g) (U) :
    f.app U = g.app U â‰« X.presheaf.map (eqToHom (by subst e; rfl)).op := by
  subst e; simp

theorem app_eq {X Y : Scheme} (f : X âŸ¶ Y) {U V : Y.Opens} (e : U = V) :
    f.app U =
      Y.presheaf.map (eqToHom e.symm).op â‰«
        f.app V â‰«
          X.presheaf.map (eqToHom (congr_arg (Opens.map f.base).obj e)).op := by
  rw [â† IsIso.inv_comp_eq, â† Functor.map_inv, f.naturality]
  cases e
  rfl

theorem eqToHom_c_app {X Y : Scheme} (e : X = Y) (U) :
    (eqToHom e).app U = eqToHom (by subst e; rfl) := by subst e; rfl

lemma presheaf_map_eqToHom_op (X : Scheme) (U V : X.Opens) (i : U = V) :
    X.presheaf.map (eqToHom i).op = eqToHom (i â–¸ rfl) := by
  rw [eqToHom_op, eqToHom_map]

instance isIso_toLRSHom {X Y : Scheme} (f : X âŸ¶ Y) [IsIso f] : IsIso f.toLRSHom :=
  forgetToLocallyRingedSpace.map_isIso f

instance isIso_base {X Y : Scheme.{u}} (f : X âŸ¶ Y) [IsIso f] : IsIso f.base :=
  Scheme.forgetToTop.map_isIso f

instance {X Y : Scheme} (f : X âŸ¶ Y) [IsIso f] (U) : IsIso (f.app U) :=
  haveI := PresheafedSpace.c_isIso_of_iso f.toPshHom
  NatIso.isIso_app_of_isIso f.c _

@[simp]
theorem inv_app {X Y : Scheme} (f : X âŸ¶ Y) [IsIso f] (U : X.Opens) :
    (inv f).app U =
      X.presheaf.map (eqToHom (show (f â‰« inv f) â»Â¹áµ U = U by rw [IsIso.hom_inv_id]; rfl)).op â‰«
        inv (f.app ((inv f) â»Â¹áµ U)) := by
  rw [IsIso.eq_comp_inv, â† Scheme.comp_app, Scheme.congr_app (IsIso.hom_inv_id f),
    Scheme.id_app, Category.id_comp]

theorem inv_appTop {X Y : Scheme} (f : X âŸ¶ Y) [IsIso f] :
    (inv f).appTop = inv (f.appTop) := by simp

/-- Copies a morphism with a different underlying map -/
def Hom.copyBase {X Y : Scheme} (f : X.Hom Y) (g : X â†’ Y) (h : f.base = g) : X âŸ¶ Y where
  base := TopCat.ofHom âŸ¨g, h â–¸ f.base.1.2âŸ©
  c := f.c â‰« (TopCat.Presheaf.pushforwardEq (by subst h; rfl) _).hom
  prop x := by
    subst h
    convert f.prop x using 4
    cat_disch

lemma Hom.copyBase_eq {X Y : Scheme} (f : X.Hom Y) (g : X â†’ Y) (h : f.base = g) :
    f.copyBase g h = f := by
  subst h
  obtain âŸ¨âŸ¨âŸ¨fâ‚, fâ‚‚âŸ©, fâ‚ƒâŸ©, fâ‚„âŸ© := f
  simp only [Hom.copyBase, LocallyRingedSpace.Hom.toShHom_mk]
  congr
  cat_disch

end Scheme

/-- The spectrum of a commutative ring, as a scheme.
-/
def Spec (R : CommRingCat) : Scheme where
  local_affine _ := âŸ¨âŸ¨âŠ¤, trivialâŸ©, R, âŸ¨(Spec.toLocallyRingedSpace.obj (op R)).restrictTopIsoâŸ©âŸ©
  toLocallyRingedSpace := Spec.locallyRingedSpaceObj R

/-- The spectrum of an unbundled ring as a scheme.

WARNING: If `R` is already an element of `CommRingCat`, you should use `Spec R` instead of
`Spec(R)`, which is secretly `Spec(â†‘R)`. -/
scoped notation3 "Spec("R")" => Spec <| .of R

theorem Spec_toLocallyRingedSpace (R : CommRingCat) :
    (Spec R).toLocallyRingedSpace = Spec.locallyRingedSpaceObj R :=
  rfl

/-- The induced map of a ring homomorphism on the ring spectra, as a morphism of schemes.
-/
def Spec.map {R S : CommRingCat} (f : R âŸ¶ S) : Spec S âŸ¶ Spec R :=
  âŸ¨Spec.locallyRingedSpaceMap fâŸ©

@[simp]
theorem Spec.map_id (R : CommRingCat) : Spec.map (ğŸ™ R) = ğŸ™ (Spec R) :=
  Scheme.Hom.ext' <| Spec.locallyRingedSpaceMap_id R

@[reassoc, simp]
theorem Spec.map_comp {R S T : CommRingCat} (f : R âŸ¶ S) (g : S âŸ¶ T) :
    Spec.map (f â‰« g) = Spec.map g â‰« Spec.map f :=
  Scheme.Hom.ext' <| Spec.locallyRingedSpaceMap_comp f g

/-- The spectrum, as a contravariant functor from commutative rings to schemes. -/
@[simps]
protected def Scheme.Spec : CommRingCatáµ’áµ– â¥¤ Scheme where
  obj R := Spec (unop R)
  map f := Spec.map f.unop
  map_id R := by simp
  map_comp f g := by simp

lemma Spec.map_eqToHom {R S : CommRingCat} (e : R = S) :
    Spec.map (eqToHom e) = eqToHom (e â–¸ rfl) := by
  subst e; exact Spec.map_id _

instance {R S : CommRingCat} (f : R âŸ¶ S) [IsIso f] : IsIso (Spec.map f) :=
  inferInstanceAs (IsIso <| Scheme.Spec.map f.op)

@[simp]
lemma Spec.map_inv {R S : CommRingCat} (f : R âŸ¶ S) [IsIso f] :
    Spec.map (inv f) = inv (Spec.map f) := by
  change Scheme.Spec.map (inv f).op = inv (Scheme.Spec.map f.op)
  rw [op_inv, â† Scheme.Spec.map_inv]

section

variable {R S : CommRingCat.{u}} (f : R âŸ¶ S)

-- The lemmas below are not tagged simp to respect the abstraction.
lemma Spec_carrier (R : CommRingCat.{u}) : (Spec R).carrier = PrimeSpectrum R := rfl
lemma Spec_sheaf (R : CommRingCat.{u}) : (Spec R).sheaf = Spec.structureSheaf R := rfl
lemma Spec_presheaf (R : CommRingCat.{u}) : (Spec R).presheaf = (Spec.structureSheaf R).1 := rfl
lemma Spec.map_base : (Spec.map f).base = ofHom (PrimeSpectrum.comap f.hom) := rfl
lemma Spec.map_base_apply (x : Spec S) : (Spec.map f).base x = PrimeSpectrum.comap f.hom x := rfl

lemma Spec.map_app (U) :
    (Spec.map f).app U =
      CommRingCat.ofHom (StructureSheaf.comap f.hom U (Spec.map f â»Â¹áµ U) le_rfl) := rfl

lemma Spec.map_appLE {U V} (e : U â‰¤ Spec.map f â»Â¹áµ V) :
    (Spec.map f).appLE V U e = CommRingCat.ofHom (StructureSheaf.comap f.hom V U e) := rfl

instance {A : CommRingCat} [Nontrivial A] : Nonempty (Spec A) :=
  inferInstanceAs <| Nonempty (PrimeSpectrum A)

end

namespace Scheme

theorem isEmpty_of_commSq {W X Y S : Scheme.{u}} {f : X âŸ¶ S} {g : Y âŸ¶ S}
    {i : W âŸ¶ X} {j : W âŸ¶ Y} (h : CommSq i j f g)
    (H : Disjoint (Set.range f.base) (Set.range g.base)) : IsEmpty W :=
  âŸ¨fun x â†¦ (Set.disjoint_iff_inter_eq_empty.mp H).le
    âŸ¨âŸ¨i.base x, congr($(h.w).base x)âŸ©, âŸ¨j.base x, rflâŸ©âŸ©âŸ©

/-- The empty scheme. -/
@[simps]
def empty : Scheme where
  carrier := TopCat.of PEmpty
  presheaf := (CategoryTheory.Functor.const _).obj (CommRingCat.of PUnit)
  IsSheaf := Presheaf.isSheaf_of_isTerminal _ CommRingCat.punitIsTerminal
  isLocalRing x := PEmpty.elim x
  local_affine x := PEmpty.elim x

instance : EmptyCollection Scheme :=
  âŸ¨emptyâŸ©

instance : Inhabited Scheme :=
  âŸ¨âˆ…âŸ©

/-- The global sections, notated Gamma.
-/
def Î“ : Schemeáµ’áµ– â¥¤ CommRingCat :=
  Scheme.forgetToLocallyRingedSpace.op â‹™ LocallyRingedSpace.Î“

theorem Î“_def : Î“ = Scheme.forgetToLocallyRingedSpace.op â‹™ LocallyRingedSpace.Î“ :=
  rfl

@[simp]
theorem Î“_obj (X : Schemeáµ’áµ–) : Î“.obj X = Î“(unop X, âŠ¤) :=
  rfl

theorem Î“_obj_op (X : Scheme) : Î“.obj (op X) = Î“(X, âŠ¤) :=
  rfl

@[simp]
theorem Î“_map {X Y : Schemeáµ’áµ–} (f : X âŸ¶ Y) : Î“.map f = f.unop.appTop :=
  rfl

theorem Î“_map_op {X Y : Scheme} (f : X âŸ¶ Y) : Î“.map f.op = f.appTop :=
  rfl

/--
The counit (`SpecÎ“Identity.inv.op`) of the adjunction `Î“ âŠ£ Spec` as an isomorphism.
This is almost never needed in practical use cases. Use `Î“SpecIso` instead.
-/
def SpecÎ“Identity : Scheme.Spec.rightOp â‹™ Scheme.Î“ â‰… ğŸ­ _ :=
  Iso.symm <| NatIso.ofComponents.{u,u,u+1,u+1}
    (fun R => asIso (StructureSheaf.toOpen R âŠ¤))
    (fun {X Y} f => by convert Spec_Î“_naturality (R := X) (S := Y) f)

variable (R : CommRingCat.{u})

/-- The global sections of `Spec R` is isomorphic to `R`. -/
def Î“SpecIso : Î“(Spec R, âŠ¤) â‰… R := SpecÎ“Identity.app R

@[simp] lemma SpecÎ“Identity_app : SpecÎ“Identity.app R = Î“SpecIso R := rfl
@[simp] lemma SpecÎ“Identity_hom_app : SpecÎ“Identity.hom.app R = (Î“SpecIso R).hom := rfl
@[simp] lemma SpecÎ“Identity_inv_app : SpecÎ“Identity.inv.app R = (Î“SpecIso R).inv := rfl

@[reassoc (attr := simp)]
lemma Î“SpecIso_naturality {R S : CommRingCat.{u}} (f : R âŸ¶ S) :
    (Spec.map f).appTop â‰« (Î“SpecIso S).hom = (Î“SpecIso R).hom â‰« f := SpecÎ“Identity.hom.naturality f

-- The RHS is not necessarily simpler than the LHS, but this direction coincides with the simp
-- direction of `NatTrans.naturality`.
@[reassoc (attr := simp)]
lemma Î“SpecIso_inv_naturality {R S : CommRingCat.{u}} (f : R âŸ¶ S) :
    f â‰« (Î“SpecIso S).inv = (Î“SpecIso R).inv â‰« (Spec.map f).appTop := SpecÎ“Identity.inv.naturality f

-- This is not marked simp to respect the abstraction
lemma Î“SpecIso_inv : (Î“SpecIso R).inv = StructureSheaf.toOpen R âŠ¤ := rfl

lemma toOpen_eq (U) :
    (by exact StructureSheaf.toOpen R U) =
    (Î“SpecIso R).inv â‰« (Spec R).presheaf.map (homOfLE le_top).op := rfl

instance {K} [Field K] : Unique Spec(K) :=
  inferInstanceAs <| Unique (PrimeSpectrum K)

@[simp]
lemma default_asIdeal {K} [Field K] : (default : Spec(K)).asIdeal = âŠ¥ := rfl

section BasicOpen

variable (X : Scheme) {V U : X.Opens} (f g : Î“(X, U))

/-- The subset of the underlying space where the given section does not vanish. -/
def basicOpen : X.Opens :=
  X.toLocallyRingedSpace.toRingedSpace.basicOpen f

theorem mem_basicOpen (x : X) (hx : x âˆˆ U) :
    x âˆˆ X.basicOpen f â†” IsUnit (X.presheaf.germ U x hx f) :=
  RingedSpace.mem_basicOpen _ _ _ _

/-- A variant of `mem_basicOpen` for bundled `x : U`. -/
@[simp]
theorem mem_basicOpen' (x : U) : â†‘x âˆˆ X.basicOpen f â†” IsUnit (X.presheaf.germ U x x.2 f) :=
  RingedSpace.mem_basicOpen _ _ _ _

/-- A variant of `mem_basicOpen` without the `x âˆˆ U` assumption. -/
theorem mem_basicOpen'' {U : X.Opens} (f : Î“(X, U)) (x : X) :
    x âˆˆ X.basicOpen f â†” âˆƒ (m : x âˆˆ U), IsUnit (X.presheaf.germ U x m f) :=
  Iff.rfl

@[simp]
theorem mem_basicOpen_top (f : Î“(X, âŠ¤)) (x : X) :
    x âˆˆ X.basicOpen f â†” IsUnit (X.presheaf.germ âŠ¤ x trivial f) :=
  RingedSpace.mem_top_basicOpen _ f x

@[simp]
theorem basicOpen_res (i : op U âŸ¶ op V) : X.basicOpen (X.presheaf.map i f) = V âŠ“ X.basicOpen f :=
  RingedSpace.basicOpen_res _ i f

-- This should fire before `basicOpen_res`.
@[simp 1100]
theorem basicOpen_res_eq (i : op U âŸ¶ op V) [IsIso i] :
    X.basicOpen (X.presheaf.map i f) = X.basicOpen f :=
  RingedSpace.basicOpen_res_eq _ i f

@[sheaf_restrict]
theorem basicOpen_le : X.basicOpen f â‰¤ U :=
  RingedSpace.basicOpen_le _ _

@[sheaf_restrict]
lemma basicOpen_restrict (i : V âŸ¶ U) (f : Î“(X, U)) :
    X.basicOpen (TopCat.Presheaf.restrict f i) â‰¤ X.basicOpen f :=
  (Scheme.basicOpen_res _ _ _).trans_le inf_le_right

@[simp]
theorem preimage_basicOpen {X Y : Scheme.{u}} (f : X âŸ¶ Y) {U : Y.Opens} (r : Î“(Y, U)) :
    f â»Â¹áµ (Y.basicOpen r) = X.basicOpen (f.app U r) :=
  LocallyRingedSpace.preimage_basicOpen f.toLRSHom r

theorem preimage_basicOpen_top {X Y : Scheme.{u}} (f : X âŸ¶ Y) (r : Î“(Y, âŠ¤)) :
    f â»Â¹áµ (Y.basicOpen r) = X.basicOpen (f.appTop r) :=
  preimage_basicOpen ..

lemma basicOpen_appLE {X Y : Scheme.{u}} (f : X âŸ¶ Y) (U : X.Opens) (V : Y.Opens) (e : U â‰¤ f â»Â¹áµ V)
    (s : Î“(Y, V)) : X.basicOpen (f.appLE V U e s) = U âŠ“ f â»Â¹áµ (Y.basicOpen s) := by
  simp only [preimage_basicOpen, Hom.appLE, CommRingCat.comp_apply]
  rw [basicOpen_res]

@[simp]
theorem basicOpen_zero (U : X.Opens) : X.basicOpen (0 : Î“(X, U)) = âŠ¥ :=
  LocallyRingedSpace.basicOpen_zero _ U

@[simp]
theorem basicOpen_mul : X.basicOpen (f * g) = X.basicOpen f âŠ“ X.basicOpen g :=
  RingedSpace.basicOpen_mul _ _ _

lemma basicOpen_pow {n : â„•} (h : 0 < n) : X.basicOpen (f ^ n) = X.basicOpen f :=
  RingedSpace.basicOpen_pow _ _ _ h

lemma basicOpen_add_le :
    X.basicOpen (f + g) â‰¤ X.basicOpen f âŠ” X.basicOpen g := by
  intro x hx
  have hxU : x âˆˆ U := X.basicOpen_le _ hx
  simp only [SetLike.mem_coe, Scheme.mem_basicOpen _ _ _ hxU, map_add, Opens.coe_sup,
    Set.mem_union] at hx âŠ¢
  exact IsLocalRing.isUnit_or_isUnit_of_isUnit_add hx

theorem basicOpen_of_isUnit {f : Î“(X, U)} (hf : IsUnit f) : X.basicOpen f = U :=
  RingedSpace.basicOpen_of_isUnit _ hf

@[simp]
theorem basicOpen_one : X.basicOpen (1 : Î“(X, U)) = U :=
  X.basicOpen_of_isUnit isUnit_one

instance algebra_section_section_basicOpen {X : Scheme} {U : X.Opens} (f : Î“(X, U)) :
    Algebra Î“(X, U) Î“(X, X.basicOpen f) :=
  (X.presheaf.map (homOfLE <| X.basicOpen_le f : _ âŸ¶ U).op).hom.toAlgebra

end BasicOpen

section ZeroLocus

variable (X : Scheme.{u})

/--
The zero locus of a set of sections `s` over an open set `U` is the closed set consisting of
the complement of `U` and of all points of `U`, where all elements of `f` vanish.
-/
def zeroLocus {U : X.Opens} (s : Set Î“(X, U)) : Set X := X.toRingedSpace.zeroLocus s

lemma zeroLocus_def {U : X.Opens} (s : Set Î“(X, U)) :
    X.zeroLocus s = â‹‚ f âˆˆ s, (X.basicOpen f).carrierá¶œ :=
  rfl

lemma zeroLocus_isClosed {U : X.Opens} (s : Set Î“(X, U)) :
    IsClosed (X.zeroLocus s) :=
  X.toRingedSpace.zeroLocus_isClosed s

lemma zeroLocus_singleton {U : X.Opens} (f : Î“(X, U)) :
    X.zeroLocus {f} = (â†‘(X.basicOpen f))á¶œ :=
  X.toRingedSpace.zeroLocus_singleton f

@[simp]
lemma zeroLocus_empty_eq_univ {U : X.Opens} :
    X.zeroLocus (âˆ… : Set Î“(X, U)) = Set.univ :=
  X.toRingedSpace.zeroLocus_empty_eq_univ

@[simp]
lemma mem_zeroLocus_iff {U : X.Opens} (s : Set Î“(X, U)) (x : X) :
    x âˆˆ X.zeroLocus s â†” âˆ€ f âˆˆ s, x âˆ‰ X.basicOpen f :=
  X.toRingedSpace.mem_zeroLocus_iff s x

lemma codisjoint_zeroLocus {U : X.Opens}
    (s : Set Î“(X, U)) : Codisjoint (X.zeroLocus s) U := by
  have (x : X) : âˆ€ f âˆˆ s, x âˆˆ X.basicOpen f â†’ x âˆˆ U := fun _ _ h â†¦ X.basicOpen_le _ h
  simpa [codisjoint_iff_le_sup, Set.ext_iff, or_iff_not_imp_left]

lemma zeroLocus_span {U : X.Opens} (s : Set Î“(X, U)) :
    X.zeroLocus (U := U) (Ideal.span s) = X.zeroLocus s := by
  ext x
  simp only [Scheme.mem_zeroLocus_iff, SetLike.mem_coe]
  refine âŸ¨fun H f hfs â†¦ H f (Ideal.subset_span hfs), fun H f â†¦ Submodule.span_induction H ?_ ?_ ?_âŸ©
  Â· simp only [Scheme.basicOpen_zero]; exact not_false
  Â· exact fun a b _ _ ha hb H â†¦ (X.basicOpen_add_le a b H).elim ha hb
  Â· simp +contextual

lemma zeroLocus_map {U V : X.Opens} (i : U â‰¤ V) (s : Set Î“(X, V)) :
    X.zeroLocus ((X.presheaf.map (homOfLE i).op).hom '' s) = X.zeroLocus s âˆª Uá¶œ := by
  ext x
  suffices (âˆ€ f âˆˆ s, x âˆˆ U â†’ x âˆ‰ X.basicOpen f) â†” x âˆˆ U â†’ (âˆ€ f âˆˆ s, x âˆ‰ X.basicOpen f) by
    simpa [or_iff_not_imp_right]
  grind

lemma zeroLocus_map_of_eq {U V : X.Opens} (i : U = V) (s : Set Î“(X, V)) :
    X.zeroLocus ((X.presheaf.map (eqToHom i).op).hom '' s) = X.zeroLocus s := by
  ext; simp

lemma zeroLocus_mono {U : X.Opens} {s t : Set Î“(X, U)} (h : s âŠ† t) :
    X.zeroLocus t âŠ† X.zeroLocus s := by
  simp only [Set.subset_def, Scheme.mem_zeroLocus_iff]
  exact fun x H f hf hxf â†¦ H f (h hf) hxf

lemma preimage_zeroLocus {X Y : Scheme.{u}} (f : X âŸ¶ Y) {U : Y.Opens} (s : Set Î“(Y, U)) :
    f.base â»Â¹' Y.zeroLocus s = X.zeroLocus ((f.app U).hom '' s) := by
  ext
  simp [â† Scheme.preimage_basicOpen]
  rfl

@[simp]
lemma zeroLocus_univ {U : X.Opens} :
    X.zeroLocus (U := U) Set.univ = (â†‘U)á¶œ := by
  ext x
  simp only [Scheme.mem_zeroLocus_iff, Set.mem_univ, forall_const, Set.mem_compl_iff,
    SetLike.mem_coe, â† not_exists, not_iff_not]
  exact âŸ¨fun âŸ¨f, hfâŸ© â†¦ X.basicOpen_le f hf, fun _ â†¦ âŸ¨1, by rwa [X.basicOpen_of_isUnit isUnit_one]âŸ©âŸ©

lemma zeroLocus_iUnion {U : X.Opens} {Î¹ : Type*} (f : Î¹ â†’ Set Î“(X, U)) :
    X.zeroLocus (â‹ƒ i, f i) = â‹‚ i, X.zeroLocus (f i) := by
  simpa [zeroLocus, AlgebraicGeometry.RingedSpace.zeroLocus] using Set.iInter_comm _

lemma zeroLocus_radical {U : X.Opens} (I : Ideal Î“(X, U)) :
    X.zeroLocus (U := U) I.radical = X.zeroLocus (U := U) I := by
  refine (X.zeroLocus_mono I.le_radical).antisymm ?_
  simp only [Set.subset_def, mem_zeroLocus_iff, SetLike.mem_coe]
  rintro x H f âŸ¨n, hnâŸ© hx
  rcases n.eq_zero_or_pos with rfl | hn'
  Â· exact H f (by simpa using I.mul_mem_left f hn) hx
  Â· exact H _ hn (X.basicOpen_pow f hn' â–¸ hx)

end ZeroLocus

end Scheme

theorem basicOpen_eq_of_affine {R : CommRingCat} (f : R) :
    (Spec R).basicOpen ((Scheme.Î“SpecIso R).inv f) = PrimeSpectrum.basicOpen f := by
  ext x
  simp only [SetLike.mem_coe, Scheme.mem_basicOpen_top]
  suffices IsUnit (StructureSheaf.toStalk R x f) â†” f âˆ‰ PrimeSpectrum.asIdeal x by exact this
  rw [â† isUnit_map_iff (StructureSheaf.stalkToFiberRingHom R x).hom,
    StructureSheaf.stalkToFiberRingHom_toStalk]
  exact
    (IsLocalization.AtPrime.isUnit_to_map_iff (Localization.AtPrime (PrimeSpectrum.asIdeal x))
        (PrimeSpectrum.asIdeal x) f :
      _)

@[simp]
theorem basicOpen_eq_of_affine' {R : CommRingCat} (f : Î“(Spec R, âŠ¤)) :
    (Spec R).basicOpen f = PrimeSpectrum.basicOpen ((Scheme.Î“SpecIso R).hom f) := by
  convert basicOpen_eq_of_affine ((Scheme.Î“SpecIso R).hom f)
  exact (Iso.hom_inv_id_apply (Scheme.Î“SpecIso R) f).symm

theorem Scheme.Spec_map_presheaf_map_eqToHom {X : Scheme} {U V : X.Opens} (h : U = V) (W) :
    (Spec.map (X.presheaf.map (eqToHom h).op)).app W = eqToHom (by cases h; simp) := by
  have : Scheme.Spec.map (X.presheaf.map (ğŸ™ (op U))).op = ğŸ™ _ := by
    rw [X.presheaf.map_id, op_id, Scheme.Spec.map_id]
  cases h
  refine (Scheme.congr_app this _).trans ?_
  simp [eqToHom_map]

lemma germ_eq_zero_of_pow_mul_eq_zero {X : Scheme.{u}} {U : Opens X} (x : U) {f s : Î“(X, U)}
    (hx : x.val âˆˆ X.basicOpen s) {n : â„•} (hf : s ^ n * f = 0) : X.presheaf.germ U x x.2 f = 0 := by
  rw [Scheme.mem_basicOpen] at hx
  have hu : IsUnit (X.presheaf.germ _ x x.2 (s ^ n)) := by
    rw [map_pow]
    exact IsUnit.pow n hx
  rw [â† hu.mul_right_eq_zero, â† map_mul, hf, map_zero]

@[reassoc (attr := simp)]
lemma Scheme.iso_hom_base_inv_base {X Y : Scheme.{u}} (e : X â‰… Y) :
    e.hom.base â‰« e.inv.base = ğŸ™ _ :=
  LocallyRingedSpace.iso_hom_base_inv_base (Scheme.forgetToLocallyRingedSpace.mapIso e)

@[simp]
lemma Scheme.iso_hom_base_inv_base_apply {X Y : Scheme.{u}} (e : X â‰… Y) (x : X) :
    (e.inv.base (e.hom.base x)) = x := by
  change (e.hom.base â‰« e.inv.base) x = ğŸ™ X.toPresheafedSpace x
  simp

@[reassoc (attr := simp)]
lemma Scheme.iso_inv_base_hom_base {X Y : Scheme.{u}} (e : X â‰… Y) :
    e.inv.base â‰« e.hom.base = ğŸ™ _ :=
  LocallyRingedSpace.iso_inv_base_hom_base (Scheme.forgetToLocallyRingedSpace.mapIso e)

@[simp]
lemma Scheme.iso_inv_base_hom_base_apply {X Y : Scheme.{u}} (e : X â‰… Y) (y : Y) :
    (e.hom.base (e.inv.base y)) = y := by
  change (e.inv.base â‰« e.hom.base) y = ğŸ™ Y.toPresheafedSpace y
  simp

theorem Spec_zeroLocus_eq_zeroLocus {R : CommRingCat} (s : Set R) :
    (Spec R).zeroLocus ((Scheme.Î“SpecIso R).inv '' s) = PrimeSpectrum.zeroLocus s := by
  ext x
  suffices (âˆ€ a âˆˆ s, x âˆ‰ PrimeSpectrum.basicOpen a) â†” x âˆˆ PrimeSpectrum.zeroLocus s by simpa
  simp [Spec_carrier, PrimeSpectrum.mem_zeroLocus, Set.subset_def,
    PrimeSpectrum.mem_basicOpen _ x]

@[simp]
theorem Spec_zeroLocus {R : CommRingCat} (s : Set Î“(Spec R, âŠ¤)) :
    (Spec R).zeroLocus s = PrimeSpectrum.zeroLocus ((Scheme.Î“SpecIso R).inv â»Â¹' s) := by
  convert Spec_zeroLocus_eq_zeroLocus ((Scheme.Î“SpecIso R).inv â»Â¹' s)
  rw [Set.image_preimage_eq]
  exact (ConcreteCategory.bijective_of_isIso (C := CommRingCat) _).2
section Stalks

namespace Scheme

variable {X Y : Scheme.{u}} (f : X âŸ¶ Y)

instance (x) : IsLocalHom (f.stalkMap x).hom :=
  f.prop x

@[simp]
lemma stalkMap_id (X : Scheme.{u}) (x : X) :
    (ğŸ™ X : X âŸ¶ X).stalkMap x = ğŸ™ (X.presheaf.stalk x) :=
  PresheafedSpace.stalkMap.id _ x

lemma stalkMap_comp {X Y Z : Scheme.{u}} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (x : X) :
    (f â‰« g : X âŸ¶ Z).stalkMap x = g.stalkMap (f.base x) â‰« f.stalkMap x :=
  PresheafedSpace.stalkMap.comp f.toPshHom g.toPshHom x

@[reassoc]
lemma stalkSpecializes_stalkMap (x x' : X)
    (h : x â¤³ x') : Y.presheaf.stalkSpecializes (f.base.hom.map_specializes h) â‰« f.stalkMap x =
      f.stalkMap x' â‰« X.presheaf.stalkSpecializes h :=
  PresheafedSpace.stalkMap.stalkSpecializes_stalkMap f.toPshHom h

lemma stalkSpecializes_stalkMap_apply (x x' : X) (h : x â¤³ x') (y) :
    f.stalkMap x (Y.presheaf.stalkSpecializes (f.base.hom.map_specializes h) y) =
      (X.presheaf.stalkSpecializes h (f.stalkMap x' y)) :=
  DFunLike.congr_fun (CommRingCat.hom_ext_iff.mp (stalkSpecializes_stalkMap f x x' h)) y

@[reassoc]
lemma stalkMap_congr (f g : X âŸ¶ Y) (hfg : f = g) (x x' : X)
    (hxx' : x = x') : f.stalkMap x â‰« (X.presheaf.stalkCongr (.of_eq hxx')).hom =
      (Y.presheaf.stalkCongr (.of_eq <| hfg â–¸ hxx' â–¸ rfl)).hom â‰« g.stalkMap x' :=
  LocallyRingedSpace.stalkMap_congr f.toLRSHom g.toLRSHom congr(($hfg).toLRSHom) x x' hxx'

@[reassoc]
lemma stalkMap_congr_hom (f g : X âŸ¶ Y) (hfg : f = g) (x : X) :
    f.stalkMap x = (Y.presheaf.stalkCongr (.of_eq <| hfg â–¸ rfl)).hom â‰« g.stalkMap x :=
  LocallyRingedSpace.stalkMap_congr_hom f.toLRSHom g.toLRSHom congr(($hfg).toLRSHom) x

@[reassoc]
lemma stalkMap_congr_point (x x' : X) (hxx' : x = x') :
    f.stalkMap x â‰« (X.presheaf.stalkCongr (.of_eq hxx')).hom =
      (Y.presheaf.stalkCongr (.of_eq <| hxx' â–¸ rfl)).hom â‰« f.stalkMap x' :=
  LocallyRingedSpace.stalkMap_congr_point f.toLRSHom x x' hxx'

@[reassoc (attr := simp)]
lemma stalkMap_hom_inv (e : X â‰… Y) (y : Y) :
    e.hom.stalkMap (e.inv.base y) â‰« e.inv.stalkMap y =
      (Y.presheaf.stalkCongr (.of_eq (by simp))).hom :=
  LocallyRingedSpace.stalkMap_hom_inv (forgetToLocallyRingedSpace.mapIso e) y

@[simp]
lemma stalkMap_hom_inv_apply (e : X â‰… Y) (y : Y) (z) :
    e.inv.stalkMap y (e.hom.stalkMap (e.inv.base y) z) =
      (Y.presheaf.stalkCongr (.of_eq (by simp))).hom z :=
  DFunLike.congr_fun (CommRingCat.hom_ext_iff.mp (stalkMap_hom_inv e y)) z

@[reassoc (attr := simp)]
lemma stalkMap_inv_hom (e : X â‰… Y) (x : X) :
    e.inv.stalkMap (e.hom.base x) â‰« e.hom.stalkMap x =
      (X.presheaf.stalkCongr (.of_eq (by simp))).hom :=
  LocallyRingedSpace.stalkMap_inv_hom (forgetToLocallyRingedSpace.mapIso e) x

@[simp]
lemma stalkMap_inv_hom_apply (e : X â‰… Y) (x : X) (y) :
    e.hom.stalkMap x (e.inv.stalkMap (e.hom.base x) y) =
      (X.presheaf.stalkCongr (.of_eq (by simp))).hom y :=
  DFunLike.congr_fun (CommRingCat.hom_ext_iff.mp (stalkMap_inv_hom e x)) y

@[reassoc (attr := simp)]
lemma stalkMap_germ (U : Y.Opens) (x : X) (hx : f.base x âˆˆ U) :
    Y.presheaf.germ U (f.base x) hx â‰« f.stalkMap x =
      f.app U â‰« X.presheaf.germ (f â»Â¹áµ U) x hx :=
  PresheafedSpace.stalkMap_germ f.toPshHom U x hx

@[simp]
lemma stalkMap_germ_apply (U : Y.Opens) (x : X) (hx : f.base x âˆˆ U) (y) :
    f.stalkMap x (Y.presheaf.germ _ (f.base x) hx y) =
      X.presheaf.germ (f â»Â¹áµ U) x hx (f.app U y) :=
  PresheafedSpace.stalkMap_germ_apply f.toPshHom U x hx y

/-- If `x = y`, the stalk maps are isomorphic. -/
noncomputable def arrowStalkMapIsoOfEq {x y : X}
    (h : x = y) : Arrow.mk (f.stalkMap x) â‰… Arrow.mk (f.stalkMap y) :=
  Arrow.isoMk (Y.presheaf.stalkCongr <| (Inseparable.of_eq h).map f.continuous)
      (X.presheaf.stalkCongr <| Inseparable.of_eq h) <| by
    simp only [Arrow.mk_left, Arrow.mk_right, Functor.id_obj, TopCat.Presheaf.stalkCongr_hom,
      Arrow.mk_hom]
    rw [Scheme.stalkSpecializes_stalkMap]

end Scheme

end Stalks

section IsLocalRing

open IsLocalRing

@[simp]
lemma Spec_closedPoint {R S : CommRingCat} [IsLocalRing R] [IsLocalRing S]
    {f : R âŸ¶ S} [IsLocalHom f.hom] : (Spec.map f).base (closedPoint S) = closedPoint R :=
  IsLocalRing.comap_closedPoint f.hom

end IsLocalRing

end AlgebraicGeometry
