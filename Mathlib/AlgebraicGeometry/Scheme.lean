/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.AlgebraicGeometry.Spec
import Mathlib.Algebra.Category.Ring.Constructions
import Mathlib.CategoryTheory.Elementwise

#align_import algebraic_geometry.Scheme from "leanprover-community/mathlib"@"88474d1b5af6d37c2ab728b757771bced7f5194c"

/-!
# The category of schemes

A scheme is a locally ringed space such that every point is contained in some open set
where there is an isomorphism of presheaves between the restriction to that open set,
and the structure sheaf of `Spec R`, for some commutative ring `R`.

A morphism of schemes is just a morphism of the underlying locally ringed spaces.

-/

-- Explicit universe annotations were used in this file to improve perfomance #12737

set_option linter.uppercaseLean3 false

universe u

noncomputable section

open TopologicalSpace

open CategoryTheory

open TopCat

open Opposite

namespace AlgebraicGeometry

/-- We define `Scheme` as an `X : LocallyRingedSpace`,
along with a proof that every point has an open neighbourhood `U`
so that the restriction of `X` to `U` is isomorphic,
as a locally ringed space, to `Spec.toLocallyRingedSpace.obj (op R)`
for some `R : CommRingCat`.
-/
structure Scheme extends LocallyRingedSpace where
  local_affine :
    ‚àÄ x : toLocallyRingedSpace,
      ‚àÉ (U : OpenNhds x) (R : CommRingCat),
        Nonempty
          (toLocallyRingedSpace.restrict U.openEmbedding ‚âÖ Spec.toLocallyRingedSpace.obj (op R))
#align algebraic_geometry.Scheme AlgebraicGeometry.Scheme

namespace Scheme

/-- A morphism between schemes is a morphism between the underlying locally ringed spaces. -/
-- @[nolint has_nonempty_instance] -- Porting note(#5171): linter not ported yet
def Hom (X Y : Scheme) : Type* :=
  X.toLocallyRingedSpace ‚ü∂ Y.toLocallyRingedSpace
#align algebraic_geometry.Scheme.hom AlgebraicGeometry.Scheme.Hom

/-- Schemes are a full subcategory of locally ringed spaces.
-/
instance : Category Scheme :=
  { InducedCategory.category Scheme.toLocallyRingedSpace with Hom := Hom }

-- porting note (#10688): added to ease automation
@[continuity]
lemma Hom.continuous {X Y : Scheme} (f : X ‚ü∂ Y) : Continuous f.1.base := f.1.base.2

/-- The structure sheaf of a scheme. -/
protected abbrev sheaf (X : Scheme) :=
  X.toSheafedSpace.sheaf
#align algebraic_geometry.Scheme.sheaf AlgebraicGeometry.Scheme.sheaf

instance : CoeSort Scheme Type* where
  coe X := X.carrier

/-- The forgetful functor from `Scheme` to `LocallyRingedSpace`. -/
@[simps!]
def forgetToLocallyRingedSpace : Scheme ‚•§ LocallyRingedSpace :=
  inducedFunctor _
-- deriving Full, Faithful -- Porting note: no delta derive handler, see https://github.com/leanprover-community/mathlib4/issues/5020
#align algebraic_geometry.Scheme.forget_to_LocallyRingedSpace AlgebraicGeometry.Scheme.forgetToLocallyRingedSpace

/-- The forget functor `Scheme ‚•§ LocallyRingedSpace` is fully faithful. -/
@[simps!]
def fullyFaithfulForgetToLocallyRingedSpace :
    forgetToLocallyRingedSpace.FullyFaithful :=
  fullyFaithfulInducedFunctor _

instance : forgetToLocallyRingedSpace.Full :=
  InducedCategory.full _

instance : forgetToLocallyRingedSpace.Faithful :=
  InducedCategory.faithful _

/-- The forgetful functor from `Scheme` to `TopCat`. -/
@[simps!]
def forgetToTop : Scheme ‚•§ TopCat :=
  Scheme.forgetToLocallyRingedSpace ‚ãô LocallyRingedSpace.forgetToTop
#align algebraic_geometry.Scheme.forget_to_Top AlgebraicGeometry.Scheme.forgetToTop

-- Porting note: Lean seems not able to find this coercion any more
instance hasCoeToTopCat : CoeOut Scheme TopCat where
  coe X := X.carrier

-- Porting note: added this unification hint just in case
/-- forgetful functor to `TopCat` is the same as coercion -/
unif_hint forgetToTop_obj_eq_coe (X : Scheme) where ‚ä¢
  forgetToTop.obj X ‚âü (X : TopCat)

@[simp]
theorem id_val_base (X : Scheme) : (ùüô X : _).1.base = ùüô _ :=
  rfl
#align algebraic_geometry.Scheme.id_val_base AlgebraicGeometry.Scheme.id_val_base

@[simp]
theorem id_app {X : Scheme} (U : (Opens X.carrier)·µí·µñ) :
    (ùüô X : _).val.c.app U =
      X.presheaf.map (eqToHom (by induction' U with U; cases U; rfl)) :=
  PresheafedSpace.id_c_app X.toPresheafedSpace U
#align algebraic_geometry.Scheme.id_app AlgebraicGeometry.Scheme.id_app

@[reassoc]
theorem comp_val {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : (f ‚â´ g).val = f.val ‚â´ g.val :=
  rfl
#align algebraic_geometry.Scheme.comp_val AlgebraicGeometry.Scheme.comp_val

@[simp, reassoc] -- reassoc lemma does not need `simp`
theorem comp_coeBase {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).val.base = f.val.base ‚â´ g.val.base :=
  rfl
#align algebraic_geometry.Scheme.comp_coe_base AlgebraicGeometry.Scheme.comp_coeBase

-- Porting note: removed elementwise attribute, as generated lemmas were trivial.
@[reassoc]
theorem comp_val_base {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).val.base = f.val.base ‚â´ g.val.base :=
  rfl
#align algebraic_geometry.Scheme.comp_val_base AlgebraicGeometry.Scheme.comp_val_base

theorem comp_val_base_apply {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) :
    (f ‚â´ g).val.base x = g.val.base (f.val.base x) := by
  simp
#align algebraic_geometry.Scheme.comp_val_base_apply AlgebraicGeometry.Scheme.comp_val_base_apply

@[simp, reassoc] -- reassoc lemma does not need `simp`
theorem comp_val_c_app {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (U) :
    (f ‚â´ g).val.c.app U = g.val.c.app U ‚â´ f.val.c.app _ :=
  rfl
#align algebraic_geometry.Scheme.comp_val_c_app AlgebraicGeometry.Scheme.comp_val_c_app

theorem congr_app {X Y : Scheme} {f g : X ‚ü∂ Y} (e : f = g) (U) :
    f.val.c.app U = g.val.c.app U ‚â´ X.presheaf.map (eqToHom (by subst e; rfl)) := by
  subst e; dsimp; simp
#align algebraic_geometry.Scheme.congr_app AlgebraicGeometry.Scheme.congr_app

theorem app_eq {X Y : Scheme} (f : X ‚ü∂ Y) {U V : Opens Y.carrier} (e : U = V) :
    f.val.c.app (op U) =
      Y.presheaf.map (eqToHom e.symm).op ‚â´
        f.val.c.app (op V) ‚â´
          X.presheaf.map (eqToHom (congr_arg (Opens.map f.val.base).obj e)).op := by
  rw [‚Üê IsIso.inv_comp_eq, ‚Üê Functor.map_inv, f.val.c.naturality, Presheaf.pushforwardObj_map]
  cases e
  rfl
#align algebraic_geometry.Scheme.app_eq AlgebraicGeometry.Scheme.app_eq

-- Porting note: in `AffineScheme.lean` file, `eqToHom_op` can't be used in `(e)rw` or `simp(_rw)`
-- when terms get very complicated. See `AlgebraicGeometry.IsAffineOpen.isLocalization_stalk_aux`.
lemma presheaf_map_eqToHom_op (X : Scheme) (U V : Opens X) (i : U = V) :
    X.presheaf.map (eqToHom i).op = eqToHom (i ‚ñ∏ rfl) := by
  rw [eqToHom_op, eqToHom_map]

instance is_locallyRingedSpace_iso {X Y : Scheme} (f : X ‚ü∂ Y) [IsIso f] :
    @IsIso LocallyRingedSpace _ _ _ f :=
  forgetToLocallyRingedSpace.map_isIso f
#align algebraic_geometry.Scheme.is_LocallyRingedSpace_iso AlgebraicGeometry.Scheme.is_locallyRingedSpace_iso

-- Porting note: need an extra instance here.
instance {X Y : Scheme} (f : X ‚ü∂ Y) [IsIso f] (U) : IsIso (f.val.c.app U) :=
  haveI := PresheafedSpace.c_isIso_of_iso f.val
  NatIso.isIso_app_of_isIso _ _

@[simp]
theorem inv_val_c_app {X Y : Scheme} (f : X ‚ü∂ Y) [IsIso f] (U : Opens X.carrier) :
    (inv f).val.c.app (op U) =
      X.presheaf.map
          (eqToHom <| by rw [IsIso.hom_inv_id]; ext1; rfl :
              (Opens.map (f ‚â´ inv f).1.base).obj U ‚ü∂ U).op ‚â´
        inv (f.val.c.app (op <| (Opens.map _).obj U)) := by
  rw [IsIso.eq_comp_inv]
  erw [‚Üê Scheme.comp_val_c_app]
  rw [Scheme.congr_app (IsIso.hom_inv_id f), Scheme.id_app, ‚Üê Functor.map_comp, eqToHom_trans,
    eqToHom_op]
#align algebraic_geometry.Scheme.inv_val_c_app AlgebraicGeometry.Scheme.inv_val_c_app

theorem inv_val_c_app_top {X Y : Scheme} (f : X ‚ü∂ Y) [IsIso f] :
    (inv f).val.c.app (op ‚ä§) = inv (f.val.c.app (op ‚ä§)) := by simp

/-- Given a morphism of schemes `f : X ‚ü∂ Y`, and open sets `U ‚äÜ Y`, `V ‚äÜ f ‚Åª¬π' U`,
this is the induced map `Œì(Y, U) ‚ü∂ Œì(X, V)`. -/
abbrev Hom.appLe {X Y : Scheme} (f : X ‚ü∂ Y) {V : Opens X.carrier} {U : Opens Y.carrier}
    (e : V ‚â§ (Opens.map f.1.base).obj U) : Y.presheaf.obj (op U) ‚ü∂ X.presheaf.obj (op V) :=
  f.1.c.app (op U) ‚â´ X.presheaf.map (homOfLE e).op
#align algebraic_geometry.Scheme.hom.app_le AlgebraicGeometry.Scheme.Hom.appLe

/-- The spectrum of a commutative ring, as a scheme.
-/
def specObj (R : CommRingCat) : Scheme where
  local_affine _ := ‚ü®‚ü®‚ä§, trivial‚ü©, R, ‚ü®(Spec.toLocallyRingedSpace.obj (op R)).restrictTopIso‚ü©‚ü©
  toLocallyRingedSpace := Spec.locallyRingedSpaceObj R
#align algebraic_geometry.Scheme.Spec_obj AlgebraicGeometry.Scheme.specObj

@[simp]
theorem specObj_toLocallyRingedSpace (R : CommRingCat) :
    (specObj R).toLocallyRingedSpace = Spec.locallyRingedSpaceObj R :=
  rfl
#align algebraic_geometry.Scheme.Spec_obj_to_LocallyRingedSpace AlgebraicGeometry.Scheme.specObj_toLocallyRingedSpace

/-- The induced map of a ring homomorphism on the ring spectra, as a morphism of schemes.
-/
def specMap {R S : CommRingCat} (f : R ‚ü∂ S) : specObj S ‚ü∂ specObj R :=
  (Spec.locallyRingedSpaceMap f : Spec.locallyRingedSpaceObj S ‚ü∂ Spec.locallyRingedSpaceObj R)
#align algebraic_geometry.Scheme.Spec_map AlgebraicGeometry.Scheme.specMap

@[simp]
theorem specMap_id (R : CommRingCat) : specMap (ùüô R) = ùüô (specObj R) :=
  Spec.locallyRingedSpaceMap_id R
#align algebraic_geometry.Scheme.Spec_map_id AlgebraicGeometry.Scheme.specMap_id

theorem specMap_comp {R S T : CommRingCat} (f : R ‚ü∂ S) (g : S ‚ü∂ T) :
    specMap (f ‚â´ g) = specMap g ‚â´ specMap f :=
  Spec.locallyRingedSpaceMap_comp f g
#align algebraic_geometry.Scheme.Spec_map_comp AlgebraicGeometry.Scheme.specMap_comp

/-- The spectrum, as a contravariant functor from commutative rings to schemes.
-/
-- Porting note: removed @[simps]
-- TODO: We need to decide whether `Spec_obj` or `Spec.obj` the simp-normal form.
-- We probably want `Spec.obj`, but note
-- `locallyRingedSpaceObj` is currently the simp-normal form of `toLocallyRingedSpace.obj`.
def Spec : CommRingCat·µí·µñ ‚•§ Scheme where
  obj R := specObj (unop R)
  map f := specMap f.unop
  map_id R := by simp
  map_comp f g := by simp [specMap_comp]
#align algebraic_geometry.Scheme.Spec AlgebraicGeometry.Scheme.Spec

/-- The empty scheme.
-/
@[simps]
def empty : Scheme where
  carrier := TopCat.of PEmpty
  presheaf := (CategoryTheory.Functor.const _).obj (CommRingCat.of PUnit)
  IsSheaf := Presheaf.isSheaf_of_isTerminal _ CommRingCat.punitIsTerminal
  localRing x := PEmpty.elim x
  local_affine x := PEmpty.elim x
#align algebraic_geometry.Scheme.empty AlgebraicGeometry.Scheme.empty

instance : EmptyCollection Scheme :=
  ‚ü®empty‚ü©

instance : Inhabited Scheme :=
  ‚ü®‚àÖ‚ü©

/-- The global sections, notated Gamma.
-/
def Œì : Scheme·µí·µñ ‚•§ CommRingCat :=
  (inducedFunctor Scheme.toLocallyRingedSpace).op ‚ãô LocallyRingedSpace.Œì
#align algebraic_geometry.Scheme.Œì AlgebraicGeometry.Scheme.Œì

theorem Œì_def : Œì = (inducedFunctor Scheme.toLocallyRingedSpace).op ‚ãô LocallyRingedSpace.Œì :=
  rfl
#align algebraic_geometry.Scheme.Œì_def AlgebraicGeometry.Scheme.Œì_def

@[simp]
theorem Œì_obj (X : Scheme·µí·µñ) : Œì.obj X = (unop X).presheaf.obj (op ‚ä§) :=
  rfl
#align algebraic_geometry.Scheme.Œì_obj AlgebraicGeometry.Scheme.Œì_obj

theorem Œì_obj_op (X : Scheme) : Œì.obj (op X) = X.presheaf.obj (op ‚ä§) :=
  rfl
#align algebraic_geometry.Scheme.Œì_obj_op AlgebraicGeometry.Scheme.Œì_obj_op

@[simp]
theorem Œì_map {X Y : Scheme·µí·µñ} (f : X ‚ü∂ Y) : Œì.map f = f.unop.1.c.app (op ‚ä§) :=
  rfl
#align algebraic_geometry.Scheme.Œì_map AlgebraicGeometry.Scheme.Œì_map

theorem Œì_map_op {X Y : Scheme} (f : X ‚ü∂ Y) : Œì.map f.op = f.1.c.app (op ‚ä§) :=
  rfl
#align algebraic_geometry.Scheme.Œì_map_op AlgebraicGeometry.Scheme.Œì_map_op

section BasicOpen

variable (X : Scheme) {V U : Opens X.carrier} (f g : X.presheaf.obj (op U))

/-- The subset of the underlying space where the given section does not vanish. -/
def basicOpen : Opens X.carrier :=
  X.toLocallyRingedSpace.toRingedSpace.basicOpen f
#align algebraic_geometry.Scheme.basic_open AlgebraicGeometry.Scheme.basicOpen

@[simp]
theorem mem_basicOpen (x : U) : ‚Üëx ‚àà X.basicOpen f ‚Üî IsUnit (X.presheaf.germ x f) :=
  RingedSpace.mem_basicOpen _ _ _
#align algebraic_geometry.Scheme.mem_basic_open AlgebraicGeometry.Scheme.mem_basicOpen

theorem mem_basicOpen_top' {U : Opens X} (f : X.presheaf.obj (op U)) (x : X.carrier) :
    x ‚àà X.basicOpen f ‚Üî ‚àÉ (m : x ‚àà U), IsUnit (X.presheaf.germ (‚ü®x, m‚ü© : U) f) := by
  fconstructor
  ¬∑ rintro ‚ü®y, hy1, rfl‚ü©
    exact ‚ü®y.2, hy1‚ü©
  ¬∑ rintro ‚ü®m, hm‚ü©
    exact ‚ü®‚ü®x, m‚ü©, hm, rfl‚ü©

@[simp]
theorem mem_basicOpen_top (f : X.presheaf.obj (op ‚ä§)) (x : X.carrier) :
    x ‚àà X.basicOpen f ‚Üî IsUnit (X.presheaf.germ (‚ü®x, trivial‚ü© : (‚ä§ : Opens _)) f) :=
  RingedSpace.mem_basicOpen _ f ‚ü®x, trivial‚ü©
#align algebraic_geometry.Scheme.mem_basic_open_top AlgebraicGeometry.Scheme.mem_basicOpen_top

@[simp]
theorem basicOpen_res (i : op U ‚ü∂ op V) : X.basicOpen (X.presheaf.map i f) = V ‚äì X.basicOpen f :=
  RingedSpace.basicOpen_res _ i f
#align algebraic_geometry.Scheme.basic_open_res AlgebraicGeometry.Scheme.basicOpen_res

-- This should fire before `basicOpen_res`.
@[simp 1100]
theorem basicOpen_res_eq (i : op U ‚ü∂ op V) [IsIso i] :
    X.basicOpen (X.presheaf.map i f) = X.basicOpen f :=
  RingedSpace.basicOpen_res_eq _ i f
#align algebraic_geometry.Scheme.basic_open_res_eq AlgebraicGeometry.Scheme.basicOpen_res_eq

@[sheaf_restrict]
theorem basicOpen_le : X.basicOpen f ‚â§ U :=
  RingedSpace.basicOpen_le _ _
#align algebraic_geometry.Scheme.basic_open_le AlgebraicGeometry.Scheme.basicOpen_le

@[sheaf_restrict]
lemma basicOpen_restrict (i : V ‚ü∂ U) (f : X.presheaf.obj (op U)) :
    X.basicOpen (f |_‚Çï i) ‚â§ X.basicOpen f :=
  (Scheme.basicOpen_res _ _ _).trans_le inf_le_right

@[simp]
theorem preimage_basicOpen {X Y : Scheme} (f : X ‚ü∂ Y) {U : Opens Y.carrier}
    (r : Y.presheaf.obj <| op U) :
    (Opens.map f.1.base).obj (Y.basicOpen r) =
      @Scheme.basicOpen X ((Opens.map f.1.base).obj U) (f.1.c.app (op U) r) :=
  LocallyRingedSpace.preimage_basicOpen f r
#align algebraic_geometry.Scheme.preimage_basic_open AlgebraicGeometry.Scheme.preimage_basicOpen

@[simp]
theorem basicOpen_zero (U : Opens X.carrier) : X.basicOpen (0 : X.presheaf.obj <| op U) = ‚ä• :=
  LocallyRingedSpace.basicOpen_zero _ U
#align algebraic_geometry.Scheme.basic_open_zero AlgebraicGeometry.Scheme.basicOpen_zero

@[simp]
theorem basicOpen_mul : X.basicOpen (f * g) = X.basicOpen f ‚äì X.basicOpen g :=
  RingedSpace.basicOpen_mul _ _ _
#align algebraic_geometry.Scheme.basic_open_mul AlgebraicGeometry.Scheme.basicOpen_mul

theorem basicOpen_of_isUnit {f : X.presheaf.obj (op U)} (hf : IsUnit f) : X.basicOpen f = U :=
  RingedSpace.basicOpen_of_isUnit _ hf
#align algebraic_geometry.Scheme.basic_open_of_is_unit AlgebraicGeometry.Scheme.basicOpen_of_isUnit

instance algebra_section_section_basicOpen {X : Scheme} {U : Opens X} (f : X.presheaf.obj (op U)) :
    Algebra (X.presheaf.obj (op U)) (X.presheaf.obj (op <| X.basicOpen f)) :=
  (X.presheaf.map (homOfLE <| X.basicOpen_le f : _ ‚ü∂ U).op).toAlgebra

end BasicOpen

end Scheme

theorem basicOpen_eq_of_affine {R : CommRingCat} (f : R) :
    (Scheme.Spec.obj <| op R).basicOpen ((SpecŒìIdentity.app R).inv f) =
      PrimeSpectrum.basicOpen f := by
  ext x
  erw [Scheme.mem_basicOpen_top]
  suffices IsUnit (StructureSheaf.toStalk R x f) ‚Üî f ‚àâ PrimeSpectrum.asIdeal x by exact this
  erw [‚Üê isUnit_map_iff (StructureSheaf.stalkToFiberRingHom R x),
    StructureSheaf.stalkToFiberRingHom_toStalk]
  exact
    (IsLocalization.AtPrime.isUnit_to_map_iff (Localization.AtPrime (PrimeSpectrum.asIdeal x))
        (PrimeSpectrum.asIdeal x) f :
      _)
#align algebraic_geometry.basic_open_eq_of_affine AlgebraicGeometry.basicOpen_eq_of_affine

@[simp]
theorem basicOpen_eq_of_affine' {R : CommRingCat}
    (f : (Spec.toSheafedSpace.obj (op R)).presheaf.obj (op ‚ä§)) :
    (Scheme.Spec.obj <| op R).basicOpen f =
      PrimeSpectrum.basicOpen ((SpecŒìIdentity.app R).hom f) := by
  convert basicOpen_eq_of_affine ((SpecŒìIdentity.app R).hom f)
  exact (Iso.hom_inv_id_apply (SpecŒìIdentity.app R) f).symm
#align algebraic_geometry.basic_open_eq_of_affine' AlgebraicGeometry.basicOpen_eq_of_affine'

theorem Scheme.Spec_map_presheaf_map_eqToHom {X : Scheme} {U V : Opens X} (h : U = V) (W) :
    (Scheme.Spec.map (X.presheaf.map (eqToHom h).op).op).val.c.app W =
      eqToHom (by cases h; induction W using Opposite.rec'; dsimp; simp) := by
  have : Scheme.Spec.map (X.presheaf.map (ùüô (op U))).op = ùüô _ := by
    rw [X.presheaf.map_id, op_id, Scheme.Spec.map_id]
  cases h
  refine (Scheme.congr_app this _).trans ?_
  simp [eqToHom_map]
#align algebraic_geometry.Scheme.Spec_map_presheaf_map_eqToHom AlgebraicGeometry.Scheme.Spec_map_presheaf_map_eqToHom

end AlgebraicGeometry
