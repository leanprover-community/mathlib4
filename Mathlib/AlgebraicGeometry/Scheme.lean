/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.AlgebraicGeometry.Spec
import Mathlib.Algebra.Category.Ring.Constructions
import Mathlib.CategoryTheory.Elementwise

/-!
# The category of schemes

A scheme is a locally ringed space such that every point is contained in some open set
where there is an isomorphism of presheaves between the restriction to that open set,
and the structure sheaf of `Spec R`, for some commutative ring `R`.

A morphism of schemes is just a morphism of the underlying locally ringed spaces.

-/

-- Explicit universe annotations were used in this file to improve performance https://github.com/leanprover-community/mathlib4/issues/12737


universe u

noncomputable section

open TopologicalSpace

open CategoryTheory

open TopCat

open Opposite

namespace AlgebraicGeometry

/-- We define `Scheme` as an `X : LocallyRingedSpace`,
along with a proof that every point has an open neighbourhood `U`
so that the restriction of `X` to `U` is isomorphic,
as a locally ringed space, to `Spec.toLocallyRingedSpace.obj (op R)`
for some `R : CommRingCat`.
-/
structure Scheme extends LocallyRingedSpace where
  local_affine :
    ‚àÄ x : toLocallyRingedSpace,
      ‚àÉ (U : OpenNhds x) (R : CommRingCat),
        Nonempty
          (toLocallyRingedSpace.restrict U.isOpenEmbedding ‚âÖ Spec.toLocallyRingedSpace.obj (op R))

namespace Scheme

instance : CoeSort Scheme Type* where
  coe X := X.carrier

open Lean PrettyPrinter.Delaborator SubExpr in
/-- Pretty printer for coercing schemes to types. -/
@[app_delab TopCat.carrier]
partial def delabAdjoinNotation : Delab := whenPPOption getPPNotation do
  guard <| (‚Üê getExpr).isAppOfArity ``TopCat.carrier 1
  withNaryArg 0 do
  guard <| (‚Üê getExpr).isAppOfArity ``PresheafedSpace.carrier 3
  withNaryArg 2 do
  guard <| (‚Üê getExpr).isAppOfArity ``SheafedSpace.toPresheafedSpace 3
  withNaryArg 2 do
  guard <| (‚Üê getExpr).isAppOfArity ``LocallyRingedSpace.toSheafedSpace 1
  withNaryArg 0 do
  guard <| (‚Üê getExpr).isAppOfArity ``Scheme.toLocallyRingedSpace 1
  withNaryArg 0 do
  `(‚Ü•$(‚Üê delab))

/-- The type of open sets of a scheme. -/
abbrev Opens (X : Scheme) : Type* := TopologicalSpace.Opens X

/-- A morphism between schemes is a morphism between the underlying locally ringed spaces. -/
structure Hom (X Y : Scheme)
  extends toLRSHom' : X.toLocallyRingedSpace.Hom Y.toLocallyRingedSpace where

/-- Cast a morphism of schemes into morphisms of local ringed spaces. -/
abbrev Hom.toLRSHom {X Y : Scheme.{u}} (f : X.Hom Y) :
    X.toLocallyRingedSpace ‚ü∂ Y.toLocallyRingedSpace :=
  f.toLRSHom'

/-- See Note [custom simps projection] -/
def Hom.Simps.toLRSHom {X Y : Scheme.{u}} (f : X.Hom Y) :
    X.toLocallyRingedSpace ‚ü∂ Y.toLocallyRingedSpace :=
  f.toLRSHom

initialize_simps_projections Hom (toLRSHom' ‚Üí toLRSHom)

/-- Schemes are a full subcategory of locally ringed spaces.
-/
instance : Category Scheme where
  Hom := Hom
  id X := Hom.mk (ùüô X.toLocallyRingedSpace)
  comp f g := Hom.mk (f.toLRSHom ‚â´ g.toLRSHom)

/-- `f ‚Åª¬π·µÅ U` is notation for `(Opens.map f.base).obj U`,
  the preimage of an open set `U` under `f`. -/
scoped[AlgebraicGeometry] notation3:90 f:91 " ‚Åª¬π·µÅ " U:90 =>
  @Prefunctor.obj (Scheme.Opens _) _ (Scheme.Opens _) _
    (Opens.map (f : Scheme.Hom _ _).base).toPrefunctor U

/-- `Œì(X, U)` is notation for `X.presheaf.obj (op U)`. -/
scoped[AlgebraicGeometry] notation3 "Œì(" X ", " U ")" =>
  (PresheafedSpace.presheaf (SheafedSpace.toPresheafedSpace
    (LocallyRingedSpace.toSheafedSpace (Scheme.toLocallyRingedSpace X)))).obj
    (op (Œ± := Scheme.Opens _) U)

instance {X : Scheme.{u}} : Subsingleton Œì(X, ‚ä•) :=
  CommRingCat.subsingleton_of_isTerminal X.sheaf.isTerminalOfEmpty

@[continuity, fun_prop]
lemma Hom.continuous {X Y : Scheme} (f : X.Hom Y) : Continuous f.base := f.base.hom.2

/-- The structure sheaf of a scheme. -/
protected abbrev sheaf (X : Scheme) :=
  X.toSheafedSpace.sheaf

namespace Hom

variable {X Y : Scheme.{u}} (f : Hom X Y) {U U' : Y.Opens} {V V' : X.Opens}

/-- Given a morphism of schemes `f : X ‚ü∂ Y`, and open `U ‚äÜ Y`,
this is the induced map `Œì(Y, U) ‚ü∂ Œì(X, f ‚Åª¬π·µÅ U)`. -/
abbrev app (U : Y.Opens) : Œì(Y, U) ‚ü∂ Œì(X, f ‚Åª¬π·µÅ U) :=
  f.c.app (op U)

/-- Given a morphism of schemes `f : X ‚ü∂ Y`,
this is the induced map `Œì(Y, ‚ä§) ‚ü∂ Œì(X, ‚ä§)`. -/
abbrev appTop : Œì(Y, ‚ä§) ‚ü∂ Œì(X, ‚ä§) :=
  f.app ‚ä§

@[reassoc]
lemma naturality (i : op U' ‚ü∂ op U) :
    Y.presheaf.map i ‚â´ f.app U = f.app U' ‚â´ X.presheaf.map ((Opens.map f.base).map i.unop).op :=
  f.c.naturality i

/-- Given a morphism of schemes `f : X ‚ü∂ Y`, and open sets `U ‚äÜ Y`, `V ‚äÜ f ‚Åª¬π' U`,
this is the induced map `Œì(Y, U) ‚ü∂ Œì(X, V)`. -/
def appLE (U : Y.Opens) (V : X.Opens) (e : V ‚â§ f ‚Åª¬π·µÅ U) : Œì(Y, U) ‚ü∂ Œì(X, V) :=
  f.app U ‚â´ X.presheaf.map (homOfLE e).op

@[reassoc (attr := simp)]
lemma appLE_map (e : V ‚â§ f ‚Åª¬π·µÅ U) (i : op V ‚ü∂ op V') :
    f.appLE U V e ‚â´ X.presheaf.map i = f.appLE U V' (i.unop.le.trans e) := by
  rw [Hom.appLE, Category.assoc, ‚Üê Functor.map_comp]
  rfl

@[reassoc]
lemma appLE_map' (e : V ‚â§ f ‚Åª¬π·µÅ U) (i : V = V') :
    f.appLE U V' (i ‚ñ∏ e) ‚â´ X.presheaf.map (eqToHom i).op = f.appLE U V e :=
  appLE_map _ _ _

@[reassoc (attr := simp)]
lemma map_appLE (e : V ‚â§ f ‚Åª¬π·µÅ U) (i : op U' ‚ü∂ op U) :
    Y.presheaf.map i ‚â´ f.appLE U V e =
      f.appLE U' V (e.trans ((Opens.map f.base).map i.unop).le) := by
  rw [Hom.appLE, f.naturality_assoc, ‚Üê Functor.map_comp]
  rfl

@[reassoc]
lemma map_appLE' (e : V ‚â§ f ‚Åª¬π·µÅ U) (i : U' = U) :
    Y.presheaf.map (eqToHom i).op ‚â´ f.appLE U' V (i ‚ñ∏ e) = f.appLE U V e :=
  map_appLE _ _ _

lemma app_eq_appLE {U : Y.Opens} :
    f.app U = f.appLE U _ le_rfl := by
  simp [Hom.appLE]

lemma appLE_eq_app {U : Y.Opens} :
    f.appLE U (f ‚Åª¬π·µÅ U) le_rfl = f.app U :=
  (app_eq_appLE f).symm

lemma appLE_congr (e : V ‚â§ f ‚Åª¬π·µÅ U) (e‚ÇÅ : U = U') (e‚ÇÇ : V = V')
    (P : ‚àÄ {R S : CommRingCat.{u}} (_ : R ‚ü∂ S), Prop) :
    P (f.appLE U V e) ‚Üî P (f.appLE U' V' (e‚ÇÅ ‚ñ∏ e‚ÇÇ ‚ñ∏ e)) := by
  subst e‚ÇÅ; subst e‚ÇÇ; rfl

/-- A morphism of schemes `f : X ‚ü∂ Y` induces a local ring homomorphism from
`Y.presheaf.stalk (f x)` to `X.presheaf.stalk x` for any `x : X`. -/
def stalkMap (x : X) : Y.presheaf.stalk (f.base x) ‚ü∂ X.presheaf.stalk x :=
  f.toLRSHom.stalkMap x

protected lemma ext {f g : X ‚ü∂ Y} (h_base : f.base = g.base)
    (h_app : ‚àÄ U, f.app U ‚â´ X.presheaf.map
      (eqToHom congr((Opens.map $h_base.symm).obj U)).op = g.app U) : f = g := by
  cases f; cases g; congr 1
  apply LocallyRingedSpace.Hom.ext'
  ext : 1
  ¬∑ exact h_base
  ¬∑ exact TopCat.Presheaf.ext (fun U ‚Ü¶ by simpa using h_app U)

/-- An alternative ext lemma for scheme morphisms. -/
protected lemma ext' {f g : X ‚ü∂ Y} (h : f.toLRSHom = g.toLRSHom) : f = g := by
  cases f; cases g; congr 1

lemma preimage_iSup {Œπ} (U : Œπ ‚Üí Opens Y) : f ‚Åª¬π·µÅ iSup U = ‚®Ü i, f ‚Åª¬π·µÅ U i :=
  Opens.ext (by simp)

lemma preimage_iSup_eq_top {Œπ} {U : Œπ ‚Üí Opens Y} (hU : iSup U = ‚ä§) :
    ‚®Ü i, f ‚Åª¬π·µÅ U i = ‚ä§ := f.preimage_iSup U ‚ñ∏ hU ‚ñ∏ rfl

lemma preimage_le_preimage_of_le {U U' : Y.Opens} (hUU' : U ‚â§ U') :
    f ‚Åª¬π·µÅ U ‚â§ f ‚Åª¬π·µÅ U' :=
  fun _ ha ‚Ü¶ hUU' ha

end Hom

@[simp]
lemma preimage_comp {X Y Z : Scheme.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (U) :
    (f ‚â´ g) ‚Åª¬π·µÅ U = f ‚Åª¬π·µÅ g ‚Åª¬π·µÅ U := rfl

/-- The forgetful functor from `Scheme` to `LocallyRingedSpace`. -/
@[simps!]
def forgetToLocallyRingedSpace : Scheme ‚•§ LocallyRingedSpace where
  obj := toLocallyRingedSpace
  map := Hom.toLRSHom

/-- The forget functor `Scheme ‚•§ LocallyRingedSpace` is fully faithful. -/
@[simps preimage_toLRSHom]
def fullyFaithfulForgetToLocallyRingedSpace :
    forgetToLocallyRingedSpace.FullyFaithful where
  preimage := Hom.mk

instance : forgetToLocallyRingedSpace.Full :=
  fullyFaithfulForgetToLocallyRingedSpace.full

instance : forgetToLocallyRingedSpace.Faithful :=
  fullyFaithfulForgetToLocallyRingedSpace.faithful

/-- The forgetful functor from `Scheme` to `TopCat`. -/
@[simps!]
def forgetToTop : Scheme ‚•§ TopCat :=
  Scheme.forgetToLocallyRingedSpace ‚ãô LocallyRingedSpace.forgetToTop

/-- An isomorphism of schemes induces a homeomorphism of the underlying topological spaces. -/
noncomputable def homeoOfIso {X Y : Scheme.{u}} (e : X ‚âÖ Y) : X ‚âÉ‚Çú Y :=
  TopCat.homeoOfIso (forgetToTop.mapIso e)

@[simp]
lemma coe_homeoOfIso {X Y : Scheme.{u}} (e : X ‚âÖ Y) :
    ‚áë(homeoOfIso e) = e.hom.base := rfl

@[simp]
lemma coe_homeoOfIso_symm {X Y : Scheme.{u}} (e : X ‚âÖ Y) :
    ‚áë(homeoOfIso e.symm) = e.inv.base := rfl

@[simp]
lemma homeoOfIso_symm {X Y : Scheme} (e : X ‚âÖ Y) :
    (homeoOfIso e).symm = homeoOfIso e.symm := rfl

lemma homeoOfIso_apply {X Y : Scheme} (e : X ‚âÖ Y) (x : X) :
    homeoOfIso e x = e.hom.base x := rfl

alias _root_.CategoryTheory.Iso.schemeIsoToHomeo := homeoOfIso

/-- An isomorphism of schemes induces a homeomorphism of the underlying topological spaces. -/
noncomputable def Hom.homeomorph {X Y : Scheme.{u}} (f : X.Hom Y) [IsIso (C := Scheme) f] :
    X ‚âÉ‚Çú Y :=
  (asIso f).schemeIsoToHomeo

@[simp]
lemma Hom.homeomorph_apply {X Y : Scheme.{u}} (f : X.Hom Y) [IsIso (C := Scheme) f] (x) :
    f.homeomorph x = f.base x := rfl

-- Porting note: Lean seems not able to find this coercion any more
instance hasCoeToTopCat : CoeOut Scheme TopCat where
  coe X := X.carrier

-- Porting note: added this unification hint just in case
/-- forgetful functor to `TopCat` is the same as coercion -/
unif_hint forgetToTop_obj_eq_coe (X : Scheme) where ‚ä¢
  forgetToTop.obj X ‚âü (X : TopCat)

/-- The forgetful functor from `Scheme` to `Type`. -/
nonrec def forget : Scheme.{u} ‚•§ Type u := Scheme.forgetToTop ‚ãô forget TopCat

/-- forgetful functor to `Scheme` is the same as coercion -/
-- Schemes are often coerced as types, and it would be useful to have definitionally equal types
-- to be reducibly equal. The alternative is to make `forget` reducible but that option has
-- poor performance consequences.
unif_hint forget_obj_eq_coe (X : Scheme) where ‚ä¢
  forget.obj X ‚âü (X : Type*)

@[simp] lemma forget_obj (X) : Scheme.forget.obj X = X := rfl
@[simp] lemma forget_map {X Y} (f : X ‚ü∂ Y) : forget.map f = (f.base : X ‚Üí Y) := rfl

@[simp]
theorem id.base (X : Scheme) : (ùüô X :).base = ùüô _ :=
  rfl

@[simp]
theorem id_app {X : Scheme} (U : X.Opens) :
    (ùüô X :).app U = ùüô _ := rfl

@[simp]
theorem id_appTop {X : Scheme} :
    (ùüô X :).appTop = ùüô _ :=
  rfl

@[reassoc]
theorem comp_toLRSHom {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).toLRSHom = f.toLRSHom ‚â´ g.toLRSHom :=
  rfl

@[simp, reassoc] -- reassoc lemma does not need `simp`
theorem comp_coeBase {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).base = f.base ‚â´ g.base :=
  rfl

@[reassoc]
theorem comp_base {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).base = f.base ‚â´ g.base :=
  rfl

theorem comp_base_apply {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) :
    (f ‚â´ g).base x = g.base (f.base x) := by
  simp

@[simp, reassoc] -- reassoc lemma does not need `simp`
theorem comp_app {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (U) :
    (f ‚â´ g).app U = g.app U ‚â´ f.app _ :=
  rfl

@[simp, reassoc] -- reassoc lemma does not need `simp`
theorem comp_appTop {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).appTop = g.appTop ‚â´ f.appTop :=
  rfl

theorem appLE_comp_appLE {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (U V W e‚ÇÅ e‚ÇÇ) :
    g.appLE U V e‚ÇÅ ‚â´ f.appLE V W e‚ÇÇ =
      (f ‚â´ g).appLE U W (e‚ÇÇ.trans ((Opens.map f.base).map (homOfLE e‚ÇÅ)).le) := by
  dsimp [Hom.appLE]
  rw [Category.assoc, f.naturality_assoc, ‚Üê Functor.map_comp]
  rfl

@[simp, reassoc] -- reassoc lemma does not need `simp`
theorem comp_appLE {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (U V e) :
    (f ‚â´ g).appLE U V e = g.app U ‚â´ f.appLE _ V e := by
  rw [g.app_eq_appLE, appLE_comp_appLE]

theorem congr_app {X Y : Scheme} {f g : X ‚ü∂ Y} (e : f = g) (U) :
    f.app U = g.app U ‚â´ X.presheaf.map (eqToHom (by subst e; rfl)).op := by
  subst e; simp

theorem app_eq {X Y : Scheme} (f : X ‚ü∂ Y) {U V : Y.Opens} (e : U = V) :
    f.app U =
      Y.presheaf.map (eqToHom e.symm).op ‚â´
        f.app V ‚â´
          X.presheaf.map (eqToHom (congr_arg (Opens.map f.base).obj e)).op := by
  rw [‚Üê IsIso.inv_comp_eq, ‚Üê Functor.map_inv, f.naturality]
  cases e
  rfl

theorem eqToHom_c_app {X Y : Scheme} (e : X = Y) (U) :
    (eqToHom e).app U = eqToHom (by subst e; rfl) := by subst e; rfl

-- Porting note: in `AffineScheme.lean` file, `eqToHom_op` can't be used in `(e)rw` or `simp(_rw)`
-- when terms get very complicated. See `AlgebraicGeometry.IsAffineOpen.isLocalization_stalk_aux`.
lemma presheaf_map_eqToHom_op (X : Scheme) (U V : X.Opens) (i : U = V) :
    X.presheaf.map (eqToHom i).op = eqToHom (i ‚ñ∏ rfl) := by
  rw [eqToHom_op, eqToHom_map]

instance isIso_toLRSHom {X Y : Scheme} (f : X ‚ü∂ Y) [IsIso f] : IsIso f.toLRSHom :=
  forgetToLocallyRingedSpace.map_isIso f

instance isIso_toPshHom {X Y : Scheme} (f : X ‚ü∂ Y) [IsIso f] : IsIso f.toPshHom :=
  inferInstanceAs (IsIso ((LocallyRingedSpace.forgetToSheafedSpace ‚ãô
    SheafedSpace.forgetToPresheafedSpace).map f.toLRSHom))

instance isIso_base {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [IsIso f] : IsIso f.base :=
  Scheme.forgetToTop.map_isIso f

-- Porting note: need an extra instance here.
instance {X Y : Scheme} (f : X ‚ü∂ Y) [IsIso f] (U) : IsIso (f.c.app U) :=
  haveI := PresheafedSpace.c_isIso_of_iso f.toPshHom
  NatIso.isIso_app_of_isIso f.c _

instance {X Y : Scheme} (f : X ‚ü∂ Y) [IsIso f] (U) : IsIso (f.app U) :=
  haveI := PresheafedSpace.c_isIso_of_iso f.toPshHom
  NatIso.isIso_app_of_isIso f.c _

@[simp]
theorem inv_app {X Y : Scheme} (f : X ‚ü∂ Y) [IsIso f] (U : X.Opens) :
    (inv f).app U =
      X.presheaf.map (eqToHom (show (f ‚â´ inv f) ‚Åª¬π·µÅ U = U by rw [IsIso.hom_inv_id]; rfl)).op ‚â´
        inv (f.app ((inv f) ‚Åª¬π·µÅ U)) := by
  rw [IsIso.eq_comp_inv, ‚Üê Scheme.comp_app, Scheme.congr_app (IsIso.hom_inv_id f),
    Scheme.id_app, Category.id_comp]

theorem inv_appTop {X Y : Scheme} (f : X ‚ü∂ Y) [IsIso f] :
    (inv f).appTop = inv (f.appTop) := by simp

@[deprecated (since := "2024-11-23")] alias inv_app_top := inv_appTop

/-- Copies a morphism with a different underlying map -/
def Hom.copyBase {X Y : Scheme} (f : X.Hom Y) (g : X ‚Üí Y) (h : f.base = g) : X ‚ü∂ Y where
  base := TopCat.ofHom ‚ü®g, h ‚ñ∏ f.base.1.2‚ü©
  c := f.c ‚â´ (TopCat.Presheaf.pushforwardEq (by subst h; rfl) _).hom
  prop x := by
    subst h
    convert f.prop x using 4
    aesop_cat

lemma Hom.copyBase_eq {X Y : Scheme} (f : X.Hom Y) (g : X ‚Üí Y) (h : f.base = g) :
    f.copyBase g h = f := by
  subst h
  obtain ‚ü®‚ü®‚ü®f‚ÇÅ, f‚ÇÇ‚ü©, f‚ÇÉ‚ü©, f‚ÇÑ‚ü© := f
  simp only [Hom.copyBase]
  congr
  aesop_cat

end Scheme

/-- The spectrum of a commutative ring, as a scheme.
-/
def Spec (R : CommRingCat) : Scheme where
  local_affine _ := ‚ü®‚ü®‚ä§, trivial‚ü©, R, ‚ü®(Spec.toLocallyRingedSpace.obj (op R)).restrictTopIso‚ü©‚ü©
  toLocallyRingedSpace := Spec.locallyRingedSpaceObj R

theorem Spec_toLocallyRingedSpace (R : CommRingCat) :
    (Spec R).toLocallyRingedSpace = Spec.locallyRingedSpaceObj R :=
  rfl

/-- The induced map of a ring homomorphism on the ring spectra, as a morphism of schemes.
-/
def Spec.map {R S : CommRingCat} (f : R ‚ü∂ S) : Spec S ‚ü∂ Spec R :=
  ‚ü®Spec.locallyRingedSpaceMap f‚ü©

@[simp]
theorem Spec.map_id (R : CommRingCat) : Spec.map (ùüô R) = ùüô (Spec R) :=
  Scheme.Hom.ext' <| Spec.locallyRingedSpaceMap_id R

@[reassoc, simp]
theorem Spec.map_comp {R S T : CommRingCat} (f : R ‚ü∂ S) (g : S ‚ü∂ T) :
    Spec.map (f ‚â´ g) = Spec.map g ‚â´ Spec.map f :=
  Scheme.Hom.ext' <| Spec.locallyRingedSpaceMap_comp f g

/-- The spectrum, as a contravariant functor from commutative rings to schemes. -/
@[simps]
protected def Scheme.Spec : CommRingCat·µí·µñ ‚•§ Scheme where
  obj R := Spec (unop R)
  map f := Spec.map f.unop
  map_id R := by simp
  map_comp f g := by simp

lemma Spec.map_eqToHom {R S : CommRingCat} (e : R = S) :
    Spec.map (eqToHom e) = eqToHom (e ‚ñ∏ rfl) := by
  subst e; exact Spec.map_id _

instance {R S : CommRingCat} (f : R ‚ü∂ S) [IsIso f] : IsIso (Spec.map f) :=
  inferInstanceAs (IsIso <| Scheme.Spec.map f.op)

@[simp]
lemma Spec.map_inv {R S : CommRingCat} (f : R ‚ü∂ S) [IsIso f] :
    Spec.map (inv f) = inv (Spec.map f) := by
  change Scheme.Spec.map (inv f).op = inv (Scheme.Spec.map f.op)
  rw [op_inv, ‚Üê Scheme.Spec.map_inv]

section

variable {R S : CommRingCat.{u}} (f : R ‚ü∂ S)

-- The lemmas below are not tagged simp to respect the abstraction.
lemma Spec_carrier (R : CommRingCat.{u}) : (Spec R).carrier = PrimeSpectrum R := rfl
lemma Spec_sheaf (R : CommRingCat.{u}) : (Spec R).sheaf = Spec.structureSheaf R := rfl
lemma Spec_presheaf (R : CommRingCat.{u}) : (Spec R).presheaf = (Spec.structureSheaf R).1 := rfl
lemma Spec.map_base : (Spec.map f).base = ofHom (PrimeSpectrum.comap f.hom) := rfl
lemma Spec.map_base_apply (x : Spec S) : (Spec.map f).base x = PrimeSpectrum.comap f.hom x := rfl

lemma Spec.map_app (U) :
    (Spec.map f).app U =
      CommRingCat.ofHom (StructureSheaf.comap f.hom U (Spec.map f ‚Åª¬π·µÅ U) le_rfl) := rfl

lemma Spec.map_appLE {U V} (e : U ‚â§ Spec.map f ‚Åª¬π·µÅ V) :
    (Spec.map f).appLE V U e = CommRingCat.ofHom (StructureSheaf.comap f.hom V U e) := rfl

instance {A : CommRingCat} [Nontrivial A] : Nonempty (Spec A) :=
  inferInstanceAs <| Nonempty (PrimeSpectrum A)

end

namespace Scheme

theorem isEmpty_of_commSq {W X Y S : Scheme.{u}} {f : X ‚ü∂ S} {g : Y ‚ü∂ S}
    {i : W ‚ü∂ X} {j : W ‚ü∂ Y} (h : CommSq i j f g)
    (H : Disjoint (Set.range f.base) (Set.range g.base)) : IsEmpty W :=
  ‚ü®fun x ‚Ü¶ (Set.disjoint_iff_inter_eq_empty.mp H).le
    ‚ü®‚ü®i.base x, congr($(h.w).base x)‚ü©, ‚ü®j.base x, rfl‚ü©‚ü©‚ü©

/-- The empty scheme. -/
@[simps]
def empty : Scheme where
  carrier := TopCat.of PEmpty
  presheaf := (CategoryTheory.Functor.const _).obj (CommRingCat.of PUnit)
  IsSheaf := Presheaf.isSheaf_of_isTerminal _ CommRingCat.punitIsTerminal
  isLocalRing x := PEmpty.elim x
  local_affine x := PEmpty.elim x

instance : EmptyCollection Scheme :=
  ‚ü®empty‚ü©

instance : Inhabited Scheme :=
  ‚ü®‚àÖ‚ü©

/-- The global sections, notated Gamma.
-/
def Œì : Scheme·µí·µñ ‚•§ CommRingCat :=
  Scheme.forgetToLocallyRingedSpace.op ‚ãô LocallyRingedSpace.Œì

theorem Œì_def : Œì = Scheme.forgetToLocallyRingedSpace.op ‚ãô LocallyRingedSpace.Œì :=
  rfl

@[simp]
theorem Œì_obj (X : Scheme·µí·µñ) : Œì.obj X = Œì(unop X, ‚ä§) :=
  rfl

theorem Œì_obj_op (X : Scheme) : Œì.obj (op X) = Œì(X, ‚ä§) :=
  rfl

@[simp]
theorem Œì_map {X Y : Scheme·µí·µñ} (f : X ‚ü∂ Y) : Œì.map f = f.unop.appTop :=
  rfl

theorem Œì_map_op {X Y : Scheme} (f : X ‚ü∂ Y) : Œì.map f.op = f.appTop :=
  rfl

/--
The counit (`SpecŒìIdentity.inv.op`) of the adjunction `Œì ‚ä£ Spec` as an isomorphism.
This is almost never needed in practical use cases. Use `ŒìSpecIso` instead.
-/
def SpecŒìIdentity : Scheme.Spec.rightOp ‚ãô Scheme.Œì ‚âÖ ùü≠ _ :=
  Iso.symm <| NatIso.ofComponents.{u,u,u+1,u+1}
    (fun R => asIso (StructureSheaf.toOpen R ‚ä§))
    (fun {X Y} f => by convert Spec_Œì_naturality (R := X) (S := Y) f)

variable (R : CommRingCat.{u})

/-- The global sections of `Spec R` is isomorphic to `R`. -/
def ŒìSpecIso : Œì(Spec R, ‚ä§) ‚âÖ R := SpecŒìIdentity.app R

@[simp] lemma SpecŒìIdentity_app : SpecŒìIdentity.app R = ŒìSpecIso R := rfl
@[simp] lemma SpecŒìIdentity_hom_app : SpecŒìIdentity.hom.app R = (ŒìSpecIso R).hom := rfl
@[simp] lemma SpecŒìIdentity_inv_app : SpecŒìIdentity.inv.app R = (ŒìSpecIso R).inv := rfl

@[reassoc (attr := simp)]
lemma ŒìSpecIso_naturality {R S : CommRingCat.{u}} (f : R ‚ü∂ S) :
    (Spec.map f).appTop ‚â´ (ŒìSpecIso S).hom = (ŒìSpecIso R).hom ‚â´ f := SpecŒìIdentity.hom.naturality f

-- The RHS is not necessarily simpler than the LHS, but this direction coincides with the simp
-- direction of `NatTrans.naturality`.
@[reassoc (attr := simp)]
lemma ŒìSpecIso_inv_naturality {R S : CommRingCat.{u}} (f : R ‚ü∂ S) :
    f ‚â´ (ŒìSpecIso S).inv = (ŒìSpecIso R).inv ‚â´ (Spec.map f).appTop := SpecŒìIdentity.inv.naturality f

-- This is not marked simp to respect the abstraction
lemma ŒìSpecIso_inv : (ŒìSpecIso R).inv = StructureSheaf.toOpen R ‚ä§ := rfl

lemma toOpen_eq (U) :
    (by exact StructureSheaf.toOpen R U) =
    (ŒìSpecIso R).inv ‚â´ (Spec R).presheaf.map (homOfLE le_top).op := rfl

instance {K} [Field K] : Unique (Spec (.of K)) :=
  inferInstanceAs <| Unique (PrimeSpectrum K)

@[simp]
lemma default_asIdeal {K} [Field K] : (default : Spec (.of K)).asIdeal = ‚ä• := rfl

section BasicOpen

variable (X : Scheme) {V U : X.Opens} (f g : Œì(X, U))

/-- The subset of the underlying space where the given section does not vanish. -/
def basicOpen : X.Opens :=
  X.toLocallyRingedSpace.toRingedSpace.basicOpen f

theorem mem_basicOpen (x : X) (hx : x ‚àà U) :
    x ‚àà X.basicOpen f ‚Üî IsUnit (X.presheaf.germ U x hx f) :=
  RingedSpace.mem_basicOpen _ _ _ _

/-- A variant of `mem_basicOpen` for bundled `x : U`. -/
@[simp]
theorem mem_basicOpen' (x : U) : ‚Üëx ‚àà X.basicOpen f ‚Üî IsUnit (X.presheaf.germ U x x.2 f) :=
  RingedSpace.mem_basicOpen _ _ _ _

/-- A variant of `mem_basicOpen` without the `x ‚àà U` assumption. -/
theorem mem_basicOpen'' {U : X.Opens} (f : Œì(X, U)) (x : X) :
    x ‚àà X.basicOpen f ‚Üî ‚àÉ (m : x ‚àà U), IsUnit (X.presheaf.germ U x m f) :=
  Iff.rfl

@[simp]
theorem mem_basicOpen_top (f : Œì(X, ‚ä§)) (x : X) :
    x ‚àà X.basicOpen f ‚Üî IsUnit (X.presheaf.germ ‚ä§ x trivial f) :=
  RingedSpace.mem_top_basicOpen _ f x

@[simp]
theorem basicOpen_res (i : op U ‚ü∂ op V) : X.basicOpen (X.presheaf.map i f) = V ‚äì X.basicOpen f :=
  RingedSpace.basicOpen_res _ i f

-- This should fire before `basicOpen_res`.
@[simp 1100]
theorem basicOpen_res_eq (i : op U ‚ü∂ op V) [IsIso i] :
    X.basicOpen (X.presheaf.map i f) = X.basicOpen f :=
  RingedSpace.basicOpen_res_eq _ i f

@[sheaf_restrict]
theorem basicOpen_le : X.basicOpen f ‚â§ U :=
  RingedSpace.basicOpen_le _ _

@[sheaf_restrict]
lemma basicOpen_restrict (i : V ‚ü∂ U) (f : Œì(X, U)) :
    X.basicOpen (TopCat.Presheaf.restrict f i) ‚â§ X.basicOpen f :=
  (Scheme.basicOpen_res _ _ _).trans_le inf_le_right

@[simp]
theorem preimage_basicOpen {X Y : Scheme.{u}} (f : X ‚ü∂ Y) {U : Y.Opens} (r : Œì(Y, U)) :
    f ‚Åª¬π·µÅ (Y.basicOpen r) = X.basicOpen (f.app U r) :=
  LocallyRingedSpace.preimage_basicOpen f.toLRSHom r

theorem preimage_basicOpen_top {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (r : Œì(Y, ‚ä§)) :
    f ‚Åª¬π·µÅ (Y.basicOpen r) = X.basicOpen (f.appTop r) :=
  preimage_basicOpen ..

lemma basicOpen_appLE {X Y : Scheme.{u}} (f : X ‚ü∂ Y) (U : X.Opens) (V : Y.Opens) (e : U ‚â§ f ‚Åª¬π·µÅ V)
    (s : Œì(Y, V)) : X.basicOpen (f.appLE V U e s) = U ‚äì f ‚Åª¬π·µÅ (Y.basicOpen s) := by
  simp only [preimage_basicOpen, Hom.appLE, CommRingCat.comp_apply]
  rw [basicOpen_res]

@[simp]
theorem basicOpen_zero (U : X.Opens) : X.basicOpen (0 : Œì(X, U)) = ‚ä• :=
  LocallyRingedSpace.basicOpen_zero _ U

@[simp]
theorem basicOpen_mul : X.basicOpen (f * g) = X.basicOpen f ‚äì X.basicOpen g :=
  RingedSpace.basicOpen_mul _ _ _

lemma basicOpen_pow {n : ‚Ñï} (h : 0 < n) : X.basicOpen (f ^ n) = X.basicOpen f :=
  RingedSpace.basicOpen_pow _ _ _ h

lemma basicOpen_add_le :
    X.basicOpen (f + g) ‚â§ X.basicOpen f ‚äî X.basicOpen g := by
  intro x hx
  have hxU : x ‚àà U := X.basicOpen_le _ hx
  simp only [SetLike.mem_coe, Scheme.mem_basicOpen _ _ _ hxU, map_add, Opens.coe_sup,
    Set.mem_union] at hx ‚ä¢
  exact IsLocalRing.isUnit_or_isUnit_of_isUnit_add hx

theorem basicOpen_of_isUnit {f : Œì(X, U)} (hf : IsUnit f) : X.basicOpen f = U :=
  RingedSpace.basicOpen_of_isUnit _ hf

@[simp]
theorem basicOpen_one : X.basicOpen (1 : Œì(X, U)) = U :=
  X.basicOpen_of_isUnit isUnit_one

instance algebra_section_section_basicOpen {X : Scheme} {U : X.Opens} (f : Œì(X, U)) :
    Algebra Œì(X, U) Œì(X, X.basicOpen f) :=
  (X.presheaf.map (homOfLE <| X.basicOpen_le f : _ ‚ü∂ U).op).hom.toAlgebra

end BasicOpen

section ZeroLocus

variable (X : Scheme.{u})

/--
The zero locus of a set of sections `s` over an open set `U` is the closed set consisting of
the complement of `U` and of all points of `U`, where all elements of `f` vanish.
-/
def zeroLocus {U : X.Opens} (s : Set Œì(X, U)) : Set X := X.toRingedSpace.zeroLocus s

lemma zeroLocus_def {U : X.Opens} (s : Set Œì(X, U)) :
    X.zeroLocus s = ‚ãÇ f ‚àà s, (X.basicOpen f).carrier·∂ú :=
  rfl

lemma zeroLocus_isClosed {U : X.Opens} (s : Set Œì(X, U)) :
    IsClosed (X.zeroLocus s) :=
  X.toRingedSpace.zeroLocus_isClosed s

lemma zeroLocus_singleton {U : X.Opens} (f : Œì(X, U)) :
    X.zeroLocus {f} = (‚Üë(X.basicOpen f))·∂ú :=
  X.toRingedSpace.zeroLocus_singleton f

@[simp]
lemma zeroLocus_empty_eq_univ {U : X.Opens} :
    X.zeroLocus (‚àÖ : Set Œì(X, U)) = Set.univ :=
  X.toRingedSpace.zeroLocus_empty_eq_univ

@[simp]
lemma mem_zeroLocus_iff {U : X.Opens} (s : Set Œì(X, U)) (x : X) :
    x ‚àà X.zeroLocus s ‚Üî ‚àÄ f ‚àà s, x ‚àâ X.basicOpen f :=
  X.toRingedSpace.mem_zeroLocus_iff s x

lemma codisjoint_zeroLocus {U : X.Opens}
    (s : Set Œì(X, U)) : Codisjoint (X.zeroLocus s) U := by
  have (x : X) : ‚àÄ f ‚àà s, x ‚àà X.basicOpen f ‚Üí x ‚àà U := fun _ _ h ‚Ü¶ X.basicOpen_le _ h
  simpa [codisjoint_iff_le_sup, Set.ext_iff, or_iff_not_imp_left]

lemma zeroLocus_span {U : X.Opens} (s : Set Œì(X, U)) :
    X.zeroLocus (U := U) (Ideal.span s) = X.zeroLocus s := by
  ext x
  simp only [Scheme.mem_zeroLocus_iff, SetLike.mem_coe]
  refine ‚ü®fun H f hfs ‚Ü¶ H f (Ideal.subset_span hfs), fun H f ‚Ü¶ Submodule.span_induction H ?_ ?_ ?_‚ü©
  ¬∑ simp only [Scheme.basicOpen_zero]; exact not_false
  ¬∑ exact fun a b _ _ ha hb H ‚Ü¶ (X.basicOpen_add_le a b H).elim ha hb
  ¬∑ simp +contextual

lemma zeroLocus_map {U V : X.Opens} (i : U ‚â§ V) (s : Set Œì(X, V)) :
    X.zeroLocus ((X.presheaf.map (homOfLE i).op).hom '' s) = X.zeroLocus s ‚à™ U·∂ú := by
  ext x
  suffices (‚àÄ f ‚àà s, x ‚àà U ‚Üí x ‚àâ X.basicOpen f) ‚Üî x ‚àà U ‚Üí (‚àÄ f ‚àà s, x ‚àâ X.basicOpen f) by
    simpa [or_iff_not_imp_right]
  grind

lemma zeroLocus_map_of_eq {U V : X.Opens} (i : U = V) (s : Set Œì(X, V)) :
    X.zeroLocus ((X.presheaf.map (eqToHom i).op).hom '' s) = X.zeroLocus s := by
  ext; simp

lemma zeroLocus_mono {U : X.Opens} {s t : Set Œì(X, U)} (h : s ‚äÜ t) :
    X.zeroLocus t ‚äÜ X.zeroLocus s := by
  simp only [Set.subset_def, Scheme.mem_zeroLocus_iff]
  exact fun x H f hf hxf ‚Ü¶ H f (h hf) hxf

lemma preimage_zeroLocus {X Y : Scheme.{u}} (f : X ‚ü∂ Y) {U : Y.Opens} (s : Set Œì(Y, U)) :
    f.base ‚Åª¬π' Y.zeroLocus s = X.zeroLocus ((f.app U).hom '' s) := by
  ext
  simp [‚Üê Scheme.preimage_basicOpen]
  rfl

@[simp]
lemma zeroLocus_univ {U : X.Opens} :
    X.zeroLocus (U := U) Set.univ = (‚ÜëU)·∂ú := by
  ext x
  simp only [Scheme.mem_zeroLocus_iff, Set.mem_univ, forall_const, Set.mem_compl_iff,
    SetLike.mem_coe, ‚Üê not_exists, not_iff_not]
  exact ‚ü®fun ‚ü®f, hf‚ü© ‚Ü¶ X.basicOpen_le f hf, fun _ ‚Ü¶ ‚ü®1, by rwa [X.basicOpen_of_isUnit isUnit_one]‚ü©‚ü©

lemma zeroLocus_iUnion {U : X.Opens} {Œπ : Type*} (f : Œπ ‚Üí Set Œì(X, U)) :
    X.zeroLocus (‚ãÉ i, f i) = ‚ãÇ i, X.zeroLocus (f i) := by
  simpa [zeroLocus, AlgebraicGeometry.RingedSpace.zeroLocus] using Set.iInter_comm _

lemma zeroLocus_radical {U : X.Opens} (I : Ideal Œì(X, U)) :
    X.zeroLocus (U := U) I.radical = X.zeroLocus (U := U) I := by
  refine (X.zeroLocus_mono I.le_radical).antisymm ?_
  simp only [Set.subset_def, mem_zeroLocus_iff, SetLike.mem_coe]
  rintro x H f ‚ü®n, hn‚ü© hx
  rcases n.eq_zero_or_pos with rfl | hn'
  ¬∑ exact H f (by simpa using I.mul_mem_left f hn) hx
  ¬∑ exact H _ hn (X.basicOpen_pow f hn' ‚ñ∏ hx)

end ZeroLocus

end Scheme

theorem basicOpen_eq_of_affine {R : CommRingCat} (f : R) :
    (Spec R).basicOpen ((Scheme.ŒìSpecIso R).inv f) = PrimeSpectrum.basicOpen f := by
  ext x
  simp only [SetLike.mem_coe, Scheme.mem_basicOpen_top]
  suffices IsUnit (StructureSheaf.toStalk R x f) ‚Üî f ‚àâ PrimeSpectrum.asIdeal x by exact this
  rw [‚Üê isUnit_map_iff (StructureSheaf.stalkToFiberRingHom R x).hom,
    StructureSheaf.stalkToFiberRingHom_toStalk]
  exact
    (IsLocalization.AtPrime.isUnit_to_map_iff (Localization.AtPrime (PrimeSpectrum.asIdeal x))
        (PrimeSpectrum.asIdeal x) f :
      _)

@[simp]
theorem basicOpen_eq_of_affine' {R : CommRingCat} (f : Œì(Spec R, ‚ä§)) :
    (Spec R).basicOpen f = PrimeSpectrum.basicOpen ((Scheme.ŒìSpecIso R).hom f) := by
  convert basicOpen_eq_of_affine ((Scheme.ŒìSpecIso R).hom f)
  exact (Iso.hom_inv_id_apply (Scheme.ŒìSpecIso R) f).symm

theorem Scheme.Spec_map_presheaf_map_eqToHom {X : Scheme} {U V : X.Opens} (h : U = V) (W) :
    (Spec.map (X.presheaf.map (eqToHom h).op)).app W = eqToHom (by cases h; simp) := by
  have : Scheme.Spec.map (X.presheaf.map (ùüô (op U))).op = ùüô _ := by
    rw [X.presheaf.map_id, op_id, Scheme.Spec.map_id]
  cases h
  refine (Scheme.congr_app this _).trans ?_
  simp [eqToHom_map]

lemma germ_eq_zero_of_pow_mul_eq_zero {X : Scheme.{u}} {U : Opens X} (x : U) {f s : Œì(X, U)}
    (hx : x.val ‚àà X.basicOpen s) {n : ‚Ñï} (hf : s ^ n * f = 0) : X.presheaf.germ U x x.2 f = 0 := by
  rw [Scheme.mem_basicOpen] at hx
  have hu : IsUnit (X.presheaf.germ _ x x.2 (s ^ n)) := by
    rw [map_pow]
    exact IsUnit.pow n hx
  rw [‚Üê hu.mul_right_eq_zero, ‚Üê map_mul, hf, map_zero]

@[reassoc (attr := simp)]
lemma Scheme.iso_hom_base_inv_base {X Y : Scheme.{u}} (e : X ‚âÖ Y) :
    e.hom.base ‚â´ e.inv.base = ùüô _ :=
  LocallyRingedSpace.iso_hom_base_inv_base (Scheme.forgetToLocallyRingedSpace.mapIso e)

@[simp]
lemma Scheme.iso_hom_base_inv_base_apply {X Y : Scheme.{u}} (e : X ‚âÖ Y) (x : X) :
    (e.inv.base (e.hom.base x)) = x := by
  change (e.hom.base ‚â´ e.inv.base) x = ùüô X.toPresheafedSpace x
  simp

@[reassoc (attr := simp)]
lemma Scheme.iso_inv_base_hom_base {X Y : Scheme.{u}} (e : X ‚âÖ Y) :
    e.inv.base ‚â´ e.hom.base = ùüô _ :=
  LocallyRingedSpace.iso_inv_base_hom_base (Scheme.forgetToLocallyRingedSpace.mapIso e)

@[simp]
lemma Scheme.iso_inv_base_hom_base_apply {X Y : Scheme.{u}} (e : X ‚âÖ Y) (y : Y) :
    (e.hom.base (e.inv.base y)) = y := by
  change (e.inv.base ‚â´ e.hom.base) y = ùüô Y.toPresheafedSpace y
  simp

theorem Spec_zeroLocus_eq_zeroLocus {R : CommRingCat} (s : Set R) :
    (Spec R).zeroLocus ((Scheme.ŒìSpecIso R).inv '' s) = PrimeSpectrum.zeroLocus s := by
  ext x
  suffices (‚àÄ a ‚àà s, x ‚àâ PrimeSpectrum.basicOpen a) ‚Üî x ‚àà PrimeSpectrum.zeroLocus s by simpa
  simp [Spec_carrier, PrimeSpectrum.mem_zeroLocus, Set.subset_def,
    PrimeSpectrum.mem_basicOpen _ x]

@[simp]
theorem Spec_zeroLocus {R : CommRingCat} (s : Set Œì(Spec R, ‚ä§)) :
    (Spec R).zeroLocus s = PrimeSpectrum.zeroLocus ((Scheme.ŒìSpecIso R).inv ‚Åª¬π' s) := by
  convert Spec_zeroLocus_eq_zeroLocus ((Scheme.ŒìSpecIso R).inv ‚Åª¬π' s)
  rw [Set.image_preimage_eq]
  exact (ConcreteCategory.bijective_of_isIso (C := CommRingCat) _).2
section Stalks

namespace Scheme

variable {X Y : Scheme.{u}} (f : X ‚ü∂ Y)

instance (x) : IsLocalHom (f.stalkMap x).hom :=
  f.prop x

@[simp]
lemma stalkMap_id (X : Scheme.{u}) (x : X) :
    (ùüô X : X ‚ü∂ X).stalkMap x = ùüô (X.presheaf.stalk x) :=
  PresheafedSpace.stalkMap.id _ x

lemma stalkMap_comp {X Y Z : Scheme.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) :
    (f ‚â´ g : X ‚ü∂ Z).stalkMap x = g.stalkMap (f.base x) ‚â´ f.stalkMap x :=
  PresheafedSpace.stalkMap.comp f.toPshHom g.toPshHom x

@[reassoc]
lemma stalkSpecializes_stalkMap (x x' : X)
    (h : x ‚§≥ x') : Y.presheaf.stalkSpecializes (f.base.hom.map_specializes h) ‚â´ f.stalkMap x =
      f.stalkMap x' ‚â´ X.presheaf.stalkSpecializes h :=
  PresheafedSpace.stalkMap.stalkSpecializes_stalkMap f.toPshHom h

lemma stalkSpecializes_stalkMap_apply (x x' : X) (h : x ‚§≥ x') (y) :
    f.stalkMap x (Y.presheaf.stalkSpecializes (f.base.hom.map_specializes h) y) =
      (X.presheaf.stalkSpecializes h (f.stalkMap x' y)) :=
  DFunLike.congr_fun (CommRingCat.hom_ext_iff.mp (stalkSpecializes_stalkMap f x x' h)) y

@[reassoc]
lemma stalkMap_congr (f g : X ‚ü∂ Y) (hfg : f = g) (x x' : X)
    (hxx' : x = x') : f.stalkMap x ‚â´ (X.presheaf.stalkCongr (.of_eq hxx')).hom =
      (Y.presheaf.stalkCongr (.of_eq <| hfg ‚ñ∏ hxx' ‚ñ∏ rfl)).hom ‚â´ g.stalkMap x' :=
  LocallyRingedSpace.stalkMap_congr f.toLRSHom g.toLRSHom congr(($hfg).toLRSHom) x x' hxx'

@[reassoc]
lemma stalkMap_congr_hom (f g : X ‚ü∂ Y) (hfg : f = g) (x : X) :
    f.stalkMap x = (Y.presheaf.stalkCongr (.of_eq <| hfg ‚ñ∏ rfl)).hom ‚â´ g.stalkMap x :=
  LocallyRingedSpace.stalkMap_congr_hom f.toLRSHom g.toLRSHom congr(($hfg).toLRSHom) x

@[reassoc]
lemma stalkMap_congr_point (x x' : X) (hxx' : x = x') :
    f.stalkMap x ‚â´ (X.presheaf.stalkCongr (.of_eq hxx')).hom =
      (Y.presheaf.stalkCongr (.of_eq <| hxx' ‚ñ∏ rfl)).hom ‚â´ f.stalkMap x' :=
  LocallyRingedSpace.stalkMap_congr_point f.toLRSHom x x' hxx'

@[reassoc (attr := simp)]
lemma stalkMap_hom_inv (e : X ‚âÖ Y) (y : Y) :
    e.hom.stalkMap (e.inv.base y) ‚â´ e.inv.stalkMap y =
      (Y.presheaf.stalkCongr (.of_eq (by simp))).hom :=
  LocallyRingedSpace.stalkMap_hom_inv (forgetToLocallyRingedSpace.mapIso e) y

@[simp]
lemma stalkMap_hom_inv_apply (e : X ‚âÖ Y) (y : Y) (z) :
    e.inv.stalkMap y (e.hom.stalkMap (e.inv.base y) z) =
      (Y.presheaf.stalkCongr (.of_eq (by simp))).hom z :=
  DFunLike.congr_fun (CommRingCat.hom_ext_iff.mp (stalkMap_hom_inv e y)) z

@[reassoc (attr := simp)]
lemma stalkMap_inv_hom (e : X ‚âÖ Y) (x : X) :
    e.inv.stalkMap (e.hom.base x) ‚â´ e.hom.stalkMap x =
      (X.presheaf.stalkCongr (.of_eq (by simp))).hom :=
  LocallyRingedSpace.stalkMap_inv_hom (forgetToLocallyRingedSpace.mapIso e) x

@[simp]
lemma stalkMap_inv_hom_apply (e : X ‚âÖ Y) (x : X) (y) :
    e.hom.stalkMap x (e.inv.stalkMap (e.hom.base x) y) =
      (X.presheaf.stalkCongr (.of_eq (by simp))).hom y :=
  DFunLike.congr_fun (CommRingCat.hom_ext_iff.mp (stalkMap_inv_hom e x)) y

@[reassoc (attr := simp)]
lemma stalkMap_germ (U : Y.Opens) (x : X) (hx : f.base x ‚àà U) :
    Y.presheaf.germ U (f.base x) hx ‚â´ f.stalkMap x =
      f.app U ‚â´ X.presheaf.germ (f ‚Åª¬π·µÅ U) x hx :=
  PresheafedSpace.stalkMap_germ f.toPshHom U x hx

@[simp]
lemma stalkMap_germ_apply (U : Y.Opens) (x : X) (hx : f.base x ‚àà U) (y) :
    f.stalkMap x (Y.presheaf.germ _ (f.base x) hx y) =
      X.presheaf.germ (f ‚Åª¬π·µÅ U) x hx (f.app U y) :=
  PresheafedSpace.stalkMap_germ_apply f.toPshHom U x hx y

/-- If `x = y`, the stalk maps are isomorphic. -/
noncomputable def arrowStalkMapIsoOfEq {x y : X}
    (h : x = y) : Arrow.mk (f.stalkMap x) ‚âÖ Arrow.mk (f.stalkMap y) :=
  Arrow.isoMk (Y.presheaf.stalkCongr <| (Inseparable.of_eq h).map f.continuous)
      (X.presheaf.stalkCongr <| Inseparable.of_eq h) <| by
    simp only [Arrow.mk_left, Arrow.mk_right, Functor.id_obj, TopCat.Presheaf.stalkCongr_hom,
      Arrow.mk_hom]
    rw [Scheme.stalkSpecializes_stalkMap]

end Scheme

end Stalks

section IsLocalRing

open IsLocalRing

@[simp]
lemma Spec_closedPoint {R S : CommRingCat} [IsLocalRing R] [IsLocalRing S]
    {f : R ‚ü∂ S} [IsLocalHom f.hom] : (Spec.map f).base (closedPoint S) = closedPoint R :=
  IsLocalRing.comap_closedPoint f.hom

end IsLocalRing

end AlgebraicGeometry
