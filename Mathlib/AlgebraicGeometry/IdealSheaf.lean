/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.Preimmersion
import Mathlib.AlgebraicGeometry.Morphisms.QuasiCompact
import Mathlib.CategoryTheory.Adjunction.Opposites

/-!
# Ideal sheaves on schemes

We define ideal sheaves of schemes and provide various constructors for it.

## Main definition
* `AlgebraicGeometry.Scheme.IdealSheafData`: A structure that contains the data to uniquely define
  an ideal sheaf, consisting of
  1. an ideal `I(U) ‚â§ Œì(X, U)` for every affine open `U`
  2. a proof that `I(D(f)) = I(U)_f` for every affine open `U` and every section `f : Œì(X, U)`.
* `AlgebraicGeometry.Scheme.IdealSheafData.ofIdeals`:
  The largest ideal sheaf contained in a family of ideals.
* `AlgebraicGeometry.Scheme.IdealSheafData.equivOfIsAffine`:
  Over affine schemes, ideal sheaves are in bijection with ideals of the global sections.
* `AlgebraicGeometry.Scheme.IdealSheafData.support`: The support of an ideal sheaf.
* `AlgebraicGeometry.Scheme.IdealSheafData.vanishingIdeal`: The vanishing ideal of a set.
* `AlgebraicGeometry.Scheme.Hom.ker`: The kernel of a morphism.
* `AlgebraicGeometry.Scheme.IdealSheafData.subscheme`: The subscheme associated to an ideal sheaf.
* `AlgebraicGeometry.Scheme.IdealSheafData.subschemeŒπ`: The inclusion from the subscheme.
* `AlgebraicGeometry.Scheme.Hom.image`: The scheme theoretical image of a morphism.
* `AlgebraicGeometry.Scheme.kerAdjunction`:
  The adjunction between taking kernels and taking the associated subscheme.

## Main results
* `AlgebraicGeometry.Scheme.IdealSheafData.gc`:
  `support` and `vanishingIdeal` forms a galois connection.
* `AlgebraicGeometry.Scheme.Hom.support_ker`: The support of a kernel of a quasi-compact morphism
  is the closure of the range.

## Implementation detail

Ideal sheaves are not yet defined in this file as actual subsheaves of `ùí™‚Çì`.
Instead, for the ease of development and application,
we define the structure `IdealSheafData` containing all necessary data to uniquely define an
ideal sheaf. This should be refactored as a constructor for ideal sheaves once they are introduced
into mathlib.

-/

open CategoryTheory TopologicalSpace

universe u

namespace AlgebraicGeometry.Scheme

variable {X : Scheme.{u}}

/--
A structure that contains the data to uniquely define an ideal sheaf, consisting of
1. an ideal `I(U) ‚â§ Œì(X, U)` for every affine open `U`
2. a proof that `I(D(f)) = I(U)_f` for every affine open `U` and every section `f : Œì(X, U)`
3. a subset of `X` equal to the support.

Also see `Scheme.IdealSheafData.mkOfMemSupportIff` for a constructor with the condition on the
support being (usually) easier to prove.
-/
structure IdealSheafData (X : Scheme.{u}) : Type u where
  /-- The component of an ideal sheaf at an affine open. -/
  ideal : ‚àÄ U : X.affineOpens, Ideal Œì(X, U)
  /-- Also see `AlgebraicGeometry.Scheme.IdealSheafData.map_ideal` -/
  map_ideal_basicOpen : ‚àÄ (U : X.affineOpens) (f : Œì(X, U)),
    (ideal U).map (X.presheaf.map (homOfLE <| X.basicOpen_le f).op).hom =
      ideal (X.affineBasicOpen f)
  /-- The support of an ideal sheaf. Use `IdealSheafData.support` instead for most occasions. -/
  supportSet : Set X := ‚ãÇ U, X.zeroLocus (U := U.1) (ideal U)
  supportSet_eq_iInter_zeroLocus : supportSet = ‚ãÇ U, X.zeroLocus (U := U.1) (ideal U) := by rfl

namespace IdealSheafData

@[ext]
protected lemma ext {I J : X.IdealSheafData} (h : I.ideal = J.ideal) : I = J := by
  obtain ‚ü®i, _, s, hs‚ü© := I
  obtain ‚ü®j, _, t, ht‚ü© := J
  subst h
  congr
  rw [hs, ht]

section Order

instance : PartialOrder (IdealSheafData X) := PartialOrder.lift ideal fun _ _ ‚Ü¶ IdealSheafData.ext

lemma le_def {I J : IdealSheafData X} : I ‚â§ J ‚Üî ‚àÄ U, I.ideal U ‚â§ J.ideal U := .rfl

instance : CompleteSemilatticeSup (IdealSheafData X) where
  sSup s :=
  { ideal := sSup (ideal '' s),
    map_ideal_basicOpen := by
      have : sSup (ideal '' s) = ‚®Ü i : s, ideal i.1 := by
        conv_lhs => rw [‚Üê Subtype.range_val (s := s), ‚Üê Set.range_comp]
        rfl
      simp only [this, iSup_apply, Ideal.map_iSup, map_ideal_basicOpen, implies_true] }
  le_sSup s x hxs := le_sSup (s := ideal '' s) ‚ü®_, hxs, rfl‚ü©
  sSup_le s i hi := sSup_le (s := ideal '' s) (Set.forall_mem_image.mpr hi)

/-- The largest ideal sheaf contained in a family of ideals. -/
def ofIdeals (I : ‚àÄ U : X.affineOpens, Ideal Œì(X, U)) : IdealSheafData X :=
  sSup { J : IdealSheafData X | J.ideal ‚â§ I }

lemma ideal_ofIdeals_le (I : ‚àÄ U : X.affineOpens, Ideal Œì(X, U)) :
    (ofIdeals I).ideal ‚â§ I :=
  sSup_le (Set.forall_mem_image.mpr fun _ ‚Ü¶ id)

/-- The galois coinsertion between ideal sheaves and arbitrary families of ideals. -/
protected def gci : GaloisCoinsertion ideal (ofIdeals (X := X)) where
  choice I hI :=
  { ideal := I
    map_ideal_basicOpen U f :=
      (ideal_ofIdeals_le I).antisymm hI ‚ñ∏ (ofIdeals I).map_ideal_basicOpen U f }
  gc _ _ := ‚ü®(le_sSup ¬∑), (le_trans ¬∑ (ideal_ofIdeals_le _))‚ü©
  u_l_le _ := sSup_le fun _ ‚Ü¶ id
  choice_eq I hI := IdealSheafData.ext (hI.antisymm (ideal_ofIdeals_le I))

lemma strictMono_ideal : StrictMono (ideal (X := X)) := IdealSheafData.gci.strictMono_l
lemma ideal_mono : Monotone (ideal (X := X)) := strictMono_ideal.monotone
lemma ofIdeals_mono : Monotone (ofIdeals (X := X)) := IdealSheafData.gci.gc.monotone_u
lemma ofIdeals_ideal (I : IdealSheafData X) : ofIdeals I.ideal = I := IdealSheafData.gci.u_l_eq _
lemma le_ofIdeals_iff {I : IdealSheafData X} {J} : I ‚â§ ofIdeals J ‚Üî I.ideal ‚â§ J :=
  IdealSheafData.gci.gc.le_iff_le.symm

instance : OrderTop (IdealSheafData X) where
  top.ideal := ‚ä§
  top.map_ideal_basicOpen := by simp [Ideal.map_top]
  top.supportSet := ‚ä•
  top.supportSet_eq_iInter_zeroLocus := by
    ext x
    simpa using (isBasis_affine_open X).exists_subset_of_mem_open (Set.mem_univ x) isOpen_univ
  le_top I U := le_top

instance : OrderBot (IdealSheafData X) where
  bot.ideal := ‚ä•
  bot.map_ideal_basicOpen := by simp [Ideal.map_top]
  bot.supportSet := ‚ä§
  bot.supportSet_eq_iInter_zeroLocus := by ext; simp
  bot_le I U := bot_le

instance : SemilatticeInf (IdealSheafData X) where
  inf I J :=
  { ideal := I.ideal ‚äì J.ideal
    map_ideal_basicOpen U f := by
      dsimp
      have : (X.presheaf.map (homOfLE (X.basicOpen_le f)).op).hom = algebraMap _ _ := rfl
      have inst := U.2.isLocalization_basicOpen f
      rw [‚Üê I.map_ideal_basicOpen U f, ‚Üê J.map_ideal_basicOpen U f, this]
      ext x
      obtain ‚ü®x, s, rfl‚ü© := IsLocalization.mk'_surjective (.powers f) x
      simp only [IsLocalization.mk'_mem_map_algebraMap_iff, Submonoid.mem_powers_iff, Ideal.mem_inf,
        exists_exists_eq_and]
      refine ‚ü®fun ‚ü®n, h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ ‚ü®‚ü®n, h‚ÇÅ‚ü©, ‚ü®n, h‚ÇÇ‚ü©‚ü©, ?_‚ü©
      rintro ‚ü®‚ü®n‚ÇÅ, h‚ÇÅ‚ü©, ‚ü®n‚ÇÇ, h‚ÇÇ‚ü©‚ü©
      refine ‚ü®n‚ÇÅ + n‚ÇÇ, ?_, ?_‚ü©
      ¬∑ rw [add_comm, pow_add, mul_assoc]; exact Ideal.mul_mem_left _ _ h‚ÇÅ
      ¬∑ rw [pow_add, mul_assoc]; exact Ideal.mul_mem_left _ _ h‚ÇÇ }
  inf_le_left I J U := inf_le_left
  inf_le_right I J U := inf_le_right
  le_inf I J K hIJ hIK U := le_inf (hIJ U) (hIK U)

instance : CompleteLattice (IdealSheafData X) where
  __ := inferInstanceAs (OrderTop (IdealSheafData X))
  __ := inferInstanceAs (OrderBot (IdealSheafData X))
  __ := inferInstanceAs (SemilatticeInf (IdealSheafData X))
  __ := inferInstanceAs (CompleteSemilatticeSup (IdealSheafData X))
  __ := IdealSheafData.gci.liftCompleteLattice

@[simp]
lemma ideal_top : ideal (X := X) ‚ä§ = ‚ä§ := rfl

@[simp]
lemma ideal_bot : ideal (X := X) ‚ä• = ‚ä• := rfl

@[simp]
lemma ideal_sup {I J : IdealSheafData X} : (I ‚äî J).ideal = I.ideal ‚äî J.ideal := rfl

@[simp]
lemma ideal_sSup {I : Set (IdealSheafData X)} : (sSup I).ideal = sSup (ideal '' I) := rfl

@[simp]
lemma ideal_iSup {Œπ : Type*} {I : Œπ ‚Üí IdealSheafData X} : (iSup I).ideal = ‚®Ü i, (I i).ideal := by
  rw [‚Üê sSup_range, ‚Üê sSup_range, ideal_sSup, ‚Üê Set.range_comp, Function.comp_def]

@[simp]
lemma ideal_inf {I J : IdealSheafData X} : (I ‚äì J).ideal = I.ideal ‚äì J.ideal := rfl

@[simp]
lemma ideal_biInf {Œπ : Type*} (I : Œπ ‚Üí IdealSheafData X) {s : Set Œπ} (hs : s.Finite) :
    (‚®Ö i ‚àà s, I i).ideal = ‚®Ö i ‚àà s, (I i).ideal := by
  refine hs.induction_on _ (by simp) fun {i s} his hs e ‚Ü¶ ?_
  simp only [iInf_insert, e, ideal_inf]

@[simp]
lemma ideal_iInf {Œπ : Type*} (I : Œπ ‚Üí IdealSheafData X) [Finite Œπ] :
    (‚®Ö i, I i).ideal = ‚®Ö i, (I i).ideal := by
  simpa using ideal_biInf I Set.finite_univ

end Order

variable (I : IdealSheafData X)

section map_ideal

/-- subsumed by `IdealSheafData.map_ideal` below. -/
private lemma map_ideal_basicOpen_of_eq
    {U V : X.affineOpens} (f : Œì(X, U)) (hV : V = X.affineBasicOpen f) :
    (I.ideal U).map (X.presheaf.map
        (homOfLE (X := X.Opens) (hV.trans_le (X.affineBasicOpen_le f))).op).hom =
      I.ideal V := by
  subst hV; exact I.map_ideal_basicOpen _ _

lemma map_ideal {U V : X.affineOpens} (h : U ‚â§ V) :
    (I.ideal V).map (X.presheaf.map (homOfLE h).op).hom = I.ideal U := by
  rw [U.2.ideal_ext_iff]
  intro x hxU
  obtain ‚ü®f, g, hfg, hxf‚ü© := exists_basicOpen_le_affine_inter U.2 V.2 x ‚ü®hxU, h hxU‚ü©
  have := I.map_ideal_basicOpen_of_eq (V := X.affineBasicOpen g) f (Subtype.ext hfg.symm)
  rw [‚Üê I.map_ideal_basicOpen] at this
  apply_fun Ideal.map (X.presheaf.germ (X.basicOpen g) x (hfg ‚ñ∏ hxf)).hom at this
  simp only [Ideal.map_map, ‚Üê RingHom.comp_apply, ‚Üê CommRingCat.hom_comp,
    affineBasicOpen_coe, X.presheaf.germ_res] at this ‚ä¢
  simp only [homOfLE_leOfHom, TopCat.Presheaf.germ_res', this]

/-- A form of `map_ideal` that is easier to rewrite with. -/
lemma map_ideal' {U V : X.affineOpens} (h : Opposite.op V.1 ‚ü∂ .op U.1) :
    (I.ideal V).map (X.presheaf.map h).hom = I.ideal U :=
  map_ideal _ _

lemma ideal_le_comap_ideal {U V : X.affineOpens} (h : U ‚â§ V) :
    I.ideal V ‚â§ (I.ideal U).comap (X.presheaf.map (homOfLE h).op).hom := by
  rw [‚Üê Ideal.map_le_iff_le_comap, ‚Üê I.map_ideal h]

end map_ideal

section support

lemma mem_supportSet_iff {I : IdealSheafData X} {x} :
    x ‚àà I.supportSet ‚Üî ‚àÄ U, x ‚àà X.zeroLocus (U := U.1) (I.ideal U) :=
  (Set.ext_iff.mp I.supportSet_eq_iInter_zeroLocus _).trans Set.mem_iInter

lemma supportSet_subset_zeroLocus (I : IdealSheafData X) (U : X.affineOpens) :
    I.supportSet ‚äÜ X.zeroLocus (U := U.1) (I.ideal U) :=
  I.supportSet_eq_iInter_zeroLocus.trans_subset (Set.iInter_subset _ _)

lemma zeroLocus_inter_subset_supportSet (I : IdealSheafData X) (U : X.affineOpens) :
    X.zeroLocus (U := U.1) (I.ideal U) ‚à© U ‚äÜ I.supportSet := by
  rw [I.supportSet_eq_iInter_zeroLocus]
  refine Set.subset_iInter fun V ‚Ü¶ ?_
  apply (X.codisjoint_zeroLocus (U := V) (I.ideal V)).symm.left_le_of_le_inf_right
  rintro x ‚ü®‚ü®hx, hxU‚ü©, hxV‚ü©
  simp only [Scheme.mem_zeroLocus_iff, SetLike.mem_coe] at hx ‚ä¢
  intro s hfU hxs
  obtain ‚ü®f, g, hfg, hxf‚ü© := exists_basicOpen_le_affine_inter U.2 V.2 x ‚ü®hxU, hxV‚ü©
  have inst := U.2.isLocalization_basicOpen f
  have := (I.map_ideal (U := X.affineBasicOpen f) (hfg.trans_le (X.basicOpen_le g))).le
    (Ideal.mem_map_of_mem _ hfU)
  rw [‚Üê I.map_ideal_basicOpen] at this
  obtain ‚ü®‚ü®s', ‚ü®_, n, rfl‚ü©‚ü©, hs'‚ü© :=
    (IsLocalization.mem_map_algebraMap_iff (.powers f) Œì(X, X.basicOpen f)).mp this
  apply_fun (x ‚àà X.basicOpen ¬∑) at hs'
  refine hx s' s'.2 ?_
  cases n <;>
    simpa [RingHom.algebraMap_toAlgebra, ‚Üê hfg, hxf, hxs, Scheme.basicOpen_pow] using hs'

lemma mem_supportSet_iff_of_mem {I : IdealSheafData X} {x} {U : X.affineOpens} (hxU : x ‚àà U.1) :
    x ‚àà I.supportSet ‚Üî x ‚àà X.zeroLocus (U := U.1) (I.ideal U) :=
  ‚ü®I.supportSet_eq_iInter_zeroLocus ‚ñ∏ fun h ‚Ü¶ Set.iInter_subset _ U h,
    fun h ‚Ü¶ I.zeroLocus_inter_subset_supportSet U ‚ü®h, hxU‚ü©‚ü©

lemma supportSet_inter (I : IdealSheafData X) (U : X.affineOpens) :
    I.supportSet ‚à© U = X.zeroLocus (U := U.1) (I.ideal U) ‚à© U := by
  ext x
  by_cases hxU : x ‚àà U.1
  ¬∑ simp [hxU, mem_supportSet_iff_of_mem hxU]
  ¬∑ simp [hxU]

lemma isClosed_supportSet (I : IdealSheafData X) : IsClosed I.supportSet := by
  rw [TopologicalSpace.IsOpenCover.isClosed_iff_coe_preimage (iSup_affineOpens_eq_top X)]
  intro U
  refine ‚ü®(X.zeroLocus (U := U.1) (I.ideal U))·∂ú, (X.zeroLocus_isClosed _).isOpen_compl, ?_‚ü©
  simp only [Set.preimage_compl, compl_inj_iff]
  apply Subtype.val_injective.image_injective
  simp [Set.image_preimage_eq_inter_range, I.supportSet_inter]

/-- The support of an ideal sheaf. Also see `IdealSheafData.mem_support_iff_of_mem`. -/
def support : Closeds X := ‚ü®I.supportSet, I.isClosed_supportSet‚ü©

lemma coe_support_eq_eq_iInter_zeroLocus :
    (I.support : Set X) = ‚ãÇ U, X.zeroLocus (U := U.1) (I.ideal U) :=
  I.supportSet_eq_iInter_zeroLocus

lemma mem_support_iff {I : IdealSheafData X} {x} :
    x ‚àà I.support ‚Üî ‚àÄ U, x ‚àà X.zeroLocus (U := U.1) (I.ideal U) :=
  (Set.ext_iff.mp I.supportSet_eq_iInter_zeroLocus _).trans Set.mem_iInter

lemma mem_support_iff_of_mem {I : IdealSheafData X} {x : X} {U : X.affineOpens} (h : x ‚àà U.1) :
    x ‚àà I.support ‚Üî x ‚àà X.zeroLocus (U := U.1) (I.ideal U) := by
  simpa [-mem_zeroLocus_iff, h] using congr(x ‚àà $(I.supportSet_inter U))

lemma coe_support_inter (I : IdealSheafData X) (U : X.affineOpens) :
    (I.support : Set X) ‚à© U = X.zeroLocus (U := U.1) (I.ideal U) ‚à© U :=
  I.supportSet_inter U

/-- Custom simps projection for `IdealSheafData`. -/
def Simps.coe_support : Set X := I.support

initialize_simps_projections IdealSheafData (supportSet ‚Üí coe_support, as_prefix coe_support)

/-- A useful constructor of `IdealSheafData`
with the condition on `supportSet` being easier to check. -/
@[simps ideal coe_support]
def mkOfMemSupportIff
    (ideal : ‚àÄ U : X.affineOpens, Ideal Œì(X, U))
    (map_ideal_basicOpen : ‚àÄ (U : X.affineOpens) (f : Œì(X, U)),
      (ideal U).map (X.presheaf.map (homOfLE <| X.basicOpen_le f).op).hom =
        ideal (X.affineBasicOpen f))
    (supportSet : Set X)
    (supportSet_inter :
      ‚àÄ U : X.affineOpens, ‚àÄ x ‚àà U.1, x ‚àà supportSet ‚Üî x ‚àà X.zeroLocus (U := U.1) (ideal U)) :
    X.IdealSheafData where
  ideal := ideal
  map_ideal_basicOpen := map_ideal_basicOpen
  supportSet := supportSet
  supportSet_eq_iInter_zeroLocus := by
    let I' : X.IdealSheafData := { ideal := ideal, map_ideal_basicOpen := map_ideal_basicOpen }
    show supportSet = I'.supportSet
    ext x
    obtain ‚ü®_, ‚ü®U, hU, rfl‚ü©, hxU, -‚ü© :=
      (isBasis_affine_open X).exists_subset_of_mem_open (Set.mem_univ x) isOpen_univ
    exact (supportSet_inter ‚ü®U, hU‚ü© x hxU).trans
      (I'.mem_support_iff_of_mem (U := ‚ü®U, hU‚ü©) hxU).symm

@[simp]
lemma support_top : support (X := X) ‚ä§ = ‚ä• := rfl

@[simp]
lemma support_bot : support (X := X) ‚ä• = ‚ä§ := rfl

lemma support_antitone : Antitone (support (X := X)) := by
  intro I J h
  rw [‚Üê SetLike.coe_subset_coe, I.coe_support_eq_eq_iInter_zeroLocus,
    J.coe_support_eq_eq_iInter_zeroLocus]
  exact Set.iInter_mono fun U ‚Ü¶ X.zeroLocus_mono (h U)

@[simp]
lemma support_eq_bot_iff : support I = ‚ä• ‚Üî I = ‚ä§ := by
  refine ‚ü®fun H ‚Ü¶ top_le_iff.mp fun U ‚Ü¶ ?_, by simp +contextual‚ü©
  have := (U.2.fromSpec_image_zeroLocus _).trans_subset
    ((zeroLocus_inter_subset_supportSet I U).trans H.le)
  simp only [Set.subset_empty_iff, Set.image_eq_empty, Closeds.coe_bot] at this
  simp [PrimeSpectrum.zeroLocus_empty_iff_eq_top.mp this]

end support

section IsAffine

/-- The ideal sheaf induced by an ideal of the global sections. -/
@[simps! ideal coe_support]
def ofIdealTop (I : Ideal Œì(X, ‚ä§)) : IdealSheafData X :=
  mkOfMemSupportIff
    (fun U ‚Ü¶ I.map (X.presheaf.map (homOfLE le_top).op).hom)
    (fun U f ‚Ü¶ by rw [Ideal.map_map, ‚Üê CommRingCat.hom_comp, ‚Üê Functor.map_comp]; rfl)
    (X.zeroLocus (U := ‚ä§) I)
    (fun U x hxU ‚Ü¶ by
      simp only [Ideal.map, zeroLocus_span, zeroLocus_map, Set.mem_union, Set.mem_compl_iff,
        SetLike.mem_coe, hxU, not_true_eq_false, iff_self_or, IsEmpty.forall_iff])

lemma le_of_isAffine [IsAffine X] {I J : IdealSheafData X}
    (H : I.ideal ‚ü®‚ä§, isAffineOpen_top X‚ü© ‚â§ J.ideal ‚ü®‚ä§, isAffineOpen_top X‚ü©) : I ‚â§ J := by
  intro U
  rw [‚Üê map_ideal (U := U) (V := ‚ü®‚ä§, isAffineOpen_top X‚ü©) I (le_top (a := U.1)),
    ‚Üê map_ideal (U := U) (V := ‚ü®‚ä§, isAffineOpen_top X‚ü©) J (le_top (a := U.1))]
  exact Ideal.map_mono H

lemma ext_of_isAffine [IsAffine X] {I J : IdealSheafData X}
    (H : I.ideal ‚ü®‚ä§, isAffineOpen_top X‚ü© = J.ideal ‚ü®‚ä§, isAffineOpen_top X‚ü©) : I = J :=
  (le_of_isAffine H.le).antisymm (le_of_isAffine H.ge)

/-- Over affine schemes, ideal sheaves are in bijection with ideals of the global sections. -/
@[simps]
def equivOfIsAffine [IsAffine X] : IdealSheafData X ‚âÉo Ideal Œì(X, ‚ä§) where
  toFun := (ideal ¬∑ ‚ü®‚ä§, isAffineOpen_top X‚ü©)
  invFun := ofIdealTop
  left_inv I := ext_of_isAffine (by simp)
  right_inv I := by simp
  map_rel_iff' {I J} := ‚ü®le_of_isAffine, (¬∑ _)‚ü©

end IsAffine

section ofIsClosed

open _root_.PrimeSpectrum TopologicalSpace

lemma Scheme.zeroLocus_radical {U : X.Opens} (I : Ideal Œì(X, U)) :
    X.zeroLocus (U := U) I.radical = X.zeroLocus (U := U) I := by
  refine (X.zeroLocus_mono I.le_radical).antisymm ?_
  simp only [Set.subset_def, mem_zeroLocus_iff, SetLike.mem_coe]
  rintro x H f ‚ü®n, hn‚ü© hx
  rcases n.eq_zero_or_pos with rfl | hn'
  ¬∑ exact H f (by simpa using I.mul_mem_left f hn) hx
  ¬∑ exact H _ hn (X.basicOpen_pow f hn' ‚ñ∏ hx)

/-- The radical of a ideal sheaf. -/
@[simps! ideal]
def radical (I : IdealSheafData X) : IdealSheafData X :=
  mkOfMemSupportIff
  (fun U ‚Ü¶ (I.ideal U).radical)
  (fun U f ‚Ü¶
    letI : Algebra Œì(X, U) Œì(X, X.affineBasicOpen f) :=
      (X.presheaf.map (homOfLE (X.basicOpen_le f)).op).hom.toAlgebra
    have : IsLocalization.Away f Œì(X, X.basicOpen f) := U.2.isLocalization_of_eq_basicOpen _ _ rfl
    (IsLocalization.map_radical (.powers f) Œì(X, X.basicOpen f) (I.ideal U)).trans
      congr($(I.map_ideal_basicOpen U f).radical))
  I.supportSet
  (fun U x hx ‚Ü¶ by
    simp only [mem_supportSet_iff_of_mem hx, SetLike.mem_coe, Scheme.zeroLocus_radical])

@[simp]
lemma support_radical (I : IdealSheafData X) : I.radical.support = I.support := rfl

/-- The nilradical of a scheme. -/
def _root_.AlgebraicGeometry.Scheme.nilradical (X : Scheme.{u}) : IdealSheafData X :=
  .radical ‚ä•

@[simp]
lemma _root_.AlgebraicGeometry.Scheme.support_nilradical (X : Scheme.{u}) :
    X.nilradical.support = ‚ä§ := rfl

lemma le_radical : I ‚â§ I.radical := fun _ ‚Ü¶ Ideal.le_radical

@[simp]
lemma radical_top : radical (X := X) ‚ä§ = ‚ä§ := top_le_iff.mp (le_radical _)

lemma radical_bot : radical ‚ä• = nilradical X := rfl

lemma radical_sup {I J : IdealSheafData X} :
    radical (I ‚äî J) = radical (radical I ‚äî radical J) := by
  ext U : 2
  exact (Ideal.radical_sup (I.ideal U) (J.ideal U))

@[simp]
lemma radical_inf {I J : IdealSheafData X} :
    radical (I ‚äì J) = (radical I ‚äì radical J) := by
  ext U : 2
  simp only [radical_ideal, ideal_inf, Pi.inf_apply, Ideal.radical_inf]

/-- The vanishing ideal sheaf of a closed set,
which is the largest ideal sheaf whose support is equal to it.
The reduced induced scheme structure on the closed set is the quotient of this ideal. -/
@[simps! ideal coe_support]
nonrec def vanishingIdeal (Z : Closeds X) : IdealSheafData X :=
  mkOfMemSupportIff
    (fun U ‚Ü¶ vanishingIdeal (U.2.fromSpec.base ‚Åª¬π' Z))
    (fun U f ‚Ü¶ by
      let F := X.presheaf.map (homOfLE (X.basicOpen_le f)).op
      apply le_antisymm
      ¬∑ rw [Ideal.map_le_iff_le_comap]
        intro x hx
        suffices ‚àÄ p, (X.affineBasicOpen f).2.fromSpec.base p ‚àà Z ‚Üí F.hom x ‚àà p.asIdeal by
          simpa [PrimeSpectrum.mem_vanishingIdeal] using this
        intro x hxZ
        refine (PrimeSpectrum.mem_vanishingIdeal _ _).mp hx
          ((Spec.map (X.presheaf.map (homOfLE _).op)).base x) ?_
        rwa [Set.mem_preimage, ‚Üê Scheme.comp_base_apply,
          IsAffineOpen.map_fromSpec _ (X.affineBasicOpen f).2]
      ¬∑ letI : Algebra Œì(X, U) Œì(X, X.affineBasicOpen f) := F.hom.toAlgebra
        have : IsLocalization.Away f Œì(X, X.basicOpen f) :=
          U.2.isLocalization_of_eq_basicOpen _ _ rfl
        intro x hx
        dsimp only at hx ‚ä¢
        have : Topology.IsOpenEmbedding (Spec.map F).base :=
          localization_away_isOpenEmbedding Œì(X, X.basicOpen f) f
        rw [‚Üê U.2.map_fromSpec (X.affineBasicOpen f).2 (homOfLE (X.basicOpen_le f)).op,
          Scheme.comp_base, TopCat.coe_comp, Set.preimage_comp] at hx
        generalize U.2.fromSpec.base ‚Åª¬π' Z = Z' at hx ‚ä¢
        replace hx : x ‚àà vanishingIdeal ((Spec.map F).base ‚Åª¬π' Z') := hx
        obtain ‚ü®I, hI, e‚ü© :=
          (isClosed_iff_zeroLocus_radical_ideal _).mp (isClosed_closure (s := Z'))
        rw [‚Üê vanishingIdeal_closure,
          ‚Üê this.isOpenMap.preimage_closure_eq_closure_preimage this.continuous, e] at hx
        rw [‚Üê vanishingIdeal_closure, e]
        erw [preimage_comap_zeroLocus] at hx
        rwa [‚Üê PrimeSpectrum.zeroLocus_span, ‚Üê Ideal.map, vanishingIdeal_zeroLocus_eq_radical,
          ‚Üê RingHom.algebraMap_toAlgebra (X.presheaf.map _).hom,
          ‚Üê IsLocalization.map_radical (.powers f), ‚Üê vanishingIdeal_zeroLocus_eq_radical] at hx)
    Z
    (fun U x hxU ‚Ü¶ by
      trans x ‚àà X.zeroLocus (U := U.1) (vanishingIdeal (U.2.fromSpec.base.hom ‚Åª¬π' Z)) ‚à© U.1
      ¬∑ rw [‚Üê U.2.fromSpec_image_zeroLocus, zeroLocus_vanishingIdeal_eq_closure,
          ‚Üê U.2.fromSpec.isOpenEmbedding.isOpenMap.preimage_closure_eq_closure_preimage
            U.2.fromSpec.base.1.2,
          Set.image_preimage_eq_inter_range, Z.isClosed.closure_eq, IsAffineOpen.range_fromSpec]
        simp [hxU]
      ¬∑ simp [hxU])

lemma subset_support_iff_le_vanishingIdeal {I : X.IdealSheafData} {Z : Closeds X} :
    (Z : Set X) ‚äÜ I.support ‚Üî I ‚â§ vanishingIdeal Z := by
  simp only [le_def, vanishingIdeal_ideal, ‚Üê PrimeSpectrum.subset_zeroLocus_iff_le_vanishingIdeal]
  trans ‚àÄ U : X.affineOpens, (Z : Set X) ‚à© U ‚äÜ I.support ‚à© U
  ¬∑ refine ‚ü®fun H U x hx ‚Ü¶ ‚ü®H hx.1, hx.2‚ü©, fun H x hx ‚Ü¶ ?_‚ü©
    obtain ‚ü®_, ‚ü®U, hU, rfl‚ü©, hxU, -‚ü© :=
      (isBasis_affine_open X).exists_subset_of_mem_open (Set.mem_univ x) isOpen_univ
    exact (H ‚ü®U, hU‚ü© ‚ü®hx, hxU‚ü©).1
  refine forall_congr' fun U ‚Ü¶ ?_
  rw [coe_support_inter, ‚Üê Set.image_subset_image_iff U.2.fromSpec.isOpenEmbedding.injective,
    Set.image_preimage_eq_inter_range, IsAffineOpen.fromSpec_image_zeroLocus,
    IsAffineOpen.range_fromSpec]

/-- `support` and `vanishingIdeal` forms a galois connection.
This is the global version of `PrimeSpectrum.gc`. -/
lemma gc : @GaloisConnection X.IdealSheafData (Closeds X)·µí·µà _ _ (support ¬∑) (vanishingIdeal ¬∑) :=
  fun _ _ ‚Ü¶ subset_support_iff_le_vanishingIdeal

lemma vanishingIdeal_antimono {S T : Closeds X} (h : S ‚â§ T) : vanishingIdeal T ‚â§ vanishingIdeal S :=
  gc.monotone_u h

lemma vanishingIdeal_support {I : IdealSheafData X} :
    vanishingIdeal I.support = I.radical := by
  ext U : 2
  dsimp
  rw [‚Üê vanishingIdeal_zeroLocus_eq_radical]
  congr 1
  apply U.2.fromSpec.isOpenEmbedding.injective.image_injective
  rw [Set.image_preimage_eq_inter_range, IsAffineOpen.range_fromSpec,
    IsAffineOpen.fromSpec_image_zeroLocus, coe_support_inter]

end ofIsClosed

end IdealSheafData

section ker

open IdealSheafData

variable {Y Z : Scheme.{u}}

/-- The kernel of a morphism,
defined as the largest (quasi-coherent) ideal sheaf contained in the component-wise kernel.
This is usually only well-behaved when `f` is quasi-compact. -/
def Hom.ker (f : X.Hom Y) : IdealSheafData Y :=
  ofIdeals fun U ‚Ü¶ RingHom.ker (f.app U).hom

lemma Hom.ideal_ker_le (f : X.Hom Y) (U : Y.affineOpens) :
    f.ker.ideal U ‚â§ RingHom.ker (f.app U).hom :=
  ideal_ofIdeals_le _ _

@[simp]
lemma Hom.ker_apply (f : X.Hom Y) [QuasiCompact f] (U : Y.affineOpens) :
    f.ker.ideal U = RingHom.ker (f.app U).hom := by
  let I : IdealSheafData Y := ‚ü®fun U ‚Ü¶ RingHom.ker (f.app U).hom, ?_, _, rfl‚ü©
  ¬∑ exact congr($(ofIdeals_ideal I).ideal U)
  intro U s
  apply le_antisymm
  ¬∑ refine Ideal.map_le_iff_le_comap.mpr fun x hx ‚Ü¶ ?_
    simp_rw [RingHom.comap_ker, ‚Üê CommRingCat.hom_comp, Scheme.affineBasicOpen_coe, f.naturality,
      CommRingCat.hom_comp, ‚Üê RingHom.comap_ker]
    exact Ideal.ker_le_comap _ hx
  ¬∑ intro x hx
    have := U.2.isLocalization_basicOpen s
    obtain ‚ü®x, ‚ü®_, n, rfl‚ü©, rfl‚ü© := IsLocalization.mk'_surjective (.powers s) x
    refine (IsLocalization.mk'_mem_map_algebraMap_iff _ _ _ _ _).mpr ?_
    suffices ‚àÉ (V : X.Opens) (hV : V = X.basicOpen ((f.app U).hom s)),
        letI := hV.trans_le (X.basicOpen_le _); ((f.app U).hom x |_ V) = 0 by
      obtain ‚ü®_, rfl, H‚ü© := this
      obtain ‚ü®n, hn‚ü© := exists_pow_mul_eq_zero_of_res_basicOpen_eq_zero_of_isCompact
        X (U := f ‚Åª¬π·µÅ U) (QuasiCompact.isCompact_preimage (f := f) _ U.1.2 U.2.isCompact)
        (f.app U x) (f.app U s) H
      exact ‚ü®_, ‚ü®n, rfl‚ü©, by simpa using hn‚ü©
    refine ‚ü®f ‚Åª¬π·µÅ Y.basicOpen s, by simp, ?_‚ü©
    replace hx : (Y.presheaf.map (homOfLE (Y.basicOpen_le s)).op ‚â´ f.app _).hom x = 0 := by
      trans (f.app (Y.basicOpen s)).hom (algebraMap Œì(Y, U) _ x)
      ¬∑ simp [-NatTrans.naturality, RingHom.algebraMap_toAlgebra]
      ¬∑ simp only [Scheme.affineBasicOpen_coe, RingHom.mem_ker] at hx
        rw [‚Üê IsLocalization.mk'_spec' (M := .powers s), map_mul, hx, mul_zero]
    rwa [f.naturality] at hx

lemma Hom.le_ker_comp (f : X ‚ü∂ Y) (g : Y.Hom Z) : g.ker ‚â§ (f ‚â´ g).ker := by
  refine ofIdeals_mono fun U ‚Ü¶ ?_
  rw [Scheme.comp_app f g U, CommRingCat.hom_comp, ‚Üê RingHom.comap_ker]
  exact Ideal.ker_le_comap _

lemma ker_eq_top_of_isEmpty (f : X.Hom Y) [IsEmpty X] : f.ker = ‚ä§ :=
  top_le_iff.mp (le_ofIdeals_iff.mpr fun U x _ ‚Ü¶ by simpa using Subsingleton.elim _ _)

lemma ker_of_isAffine {X Y : Scheme} (f : X ‚ü∂ Y) [IsAffine Y] :
    f.ker = ofIdealTop (RingHom.ker f.appTop.hom) := by
  refine (le_of_isAffine ((f.ideal_ker_le _).trans (by simp))).antisymm
    (le_ofIdeals_iff.mpr fun U ‚Ü¶ ?_)
  simp only [ofIdealTop_ideal, homOfLE_leOfHom, Ideal.map_le_iff_le_comap, RingHom.comap_ker,
    ‚Üê CommRingCat.hom_comp, f.naturality]
  intro x
  simp +contextual

lemma Hom.range_subset_ker_support (f : X.Hom Y) :
    Set.range f.base ‚äÜ f.ker.support := by
  rintro _ ‚ü®x, rfl‚ü©
  obtain ‚ü®_, ‚ü®U, hU, rfl‚ü©, hxU, -‚ü© :=
    (isBasis_affine_open Y).exists_subset_of_mem_open (Set.mem_univ (f.base x)) isOpen_univ
  refine ((coe_support_inter f.ker ‚ü®U, hU‚ü©).ge ‚ü®?_, hxU‚ü©).1
  simp only [Scheme.mem_zeroLocus_iff, SetLike.mem_coe]
  intro s hs hxs
  have : x ‚àà f ‚Åª¬π·µÅ Y.basicOpen s := hxs
  rwa [Scheme.preimage_basicOpen, RingHom.mem_ker.mp (f.ideal_ker_le _ hs),
    Scheme.basicOpen_zero] at this

lemma Hom.iInf_ker_openCover_map_comp_apply
    (f : X.Hom Y) [QuasiCompact f] (ùí∞ : X.OpenCover) (U : Y.affineOpens) :
    ‚®Ö i, (ùí∞.map i ‚â´ f).ker.ideal U = f.ker.ideal U := by
  refine le_antisymm ?_ (le_iInf fun i ‚Ü¶ (ùí∞.map i).le_ker_comp f U)
  intro s hs
  simp only [Hom.ker_apply, RingHom.mem_ker]
  apply X.IsSheaf.section_ext
  rintro x hxU
  obtain ‚ü®i, x, rfl‚ü© := ùí∞.exists_eq x
  simp only [homOfLE_leOfHom, CommRingCat.forget_map, map_zero, exists_and_left]
  refine ‚ü®ùí∞.map i ''·µÅ ùí∞.map i ‚Åª¬π·µÅ f ‚Åª¬π·µÅ U.1, ‚ü®_, hxU, rfl‚ü©,
    Set.image_preimage_subset (ùí∞.map i).base (f ‚Åª¬π·µÅ U), ?_‚ü©
  apply ((ùí∞.map i).appIso _).commRingCatIsoToRingEquiv.injective
  rw [map_zero, ‚Üê RingEquiv.coe_toRingHom, Iso.commRingCatIsoToRingEquiv_toRingHom,
    Scheme.Hom.appIso_hom']
  simp only [homOfLE_leOfHom, Scheme.Hom.app_eq_appLE, ‚Üê RingHom.comp_apply,
    ‚Üê CommRingCat.hom_comp, Scheme.Hom.appLE_map, Scheme.appLE_comp_appLE]
  simpa [Scheme.Hom.appLE] using ideal_ker_le _ _ (Ideal.mem_iInf.mp hs i)

lemma Hom.iInf_ker_openCover_map_comp (f : X ‚ü∂ Y) [QuasiCompact f] (ùí∞ : X.OpenCover) :
    ‚®Ö i, (ùí∞.map i ‚â´ f).ker = f.ker := by
  refine le_antisymm ?_ (le_iInf fun i ‚Ü¶ (ùí∞.map i).le_ker_comp f)
  refine iInf_le_iff.mpr fun I hI U ‚Ü¶ ?_
  rw [‚Üê f.iInf_ker_openCover_map_comp_apply ùí∞, le_iInf_iff]
  exact fun i ‚Ü¶ hI i U

lemma Hom.iUnion_support_ker_openCover_map_comp
    (f : X.Hom Y) [QuasiCompact f] (ùí∞ : X.OpenCover) [Finite ùí∞.J] :
    ‚ãÉ i, ((ùí∞.map i ‚â´ f).ker.support : Set Y) = f.ker.support := by
  cases isEmpty_or_nonempty ùí∞.J
  ¬∑ have : IsEmpty X := Function.isEmpty ùí∞.f
    simp [ker_eq_top_of_isEmpty]
  suffices ‚àÄ U : Y.affineOpens,
      (‚ãÉ i, (ùí∞.map i ‚â´ f).ker.support) ‚à© U = (f.ker.support ‚à© U : Set Y) by
    ext x
    obtain ‚ü®_, ‚ü®U, hU, rfl‚ü©, hxU, -‚ü© :=
      (isBasis_affine_open Y).exists_subset_of_mem_open (Set.mem_univ x) isOpen_univ
    simpa [hxU] using congr(x ‚àà $(this ‚ü®U, hU‚ü©))
  intro U
  simp only [Set.iUnion_inter, coe_support_inter, ‚Üê f.iInf_ker_openCover_map_comp_apply ùí∞,
    Scheme.zeroLocus_iInf_of_nonempty]

lemma ker_morphismRestrict_ideal (f : X.Hom Y) [QuasiCompact f]
    (U : Y.Opens) (V : U.toScheme.affineOpens) :
    (f ‚à£_ U).ker.ideal V = f.ker.ideal ‚ü®U.Œπ ''·µÅ V, V.2.image_of_isOpenImmersion _‚ü© := by
  have inst : QuasiCompact (f ‚à£_ U) := MorphismProperty.of_isPullback
      (isPullback_morphismRestrict _ _).flip inferInstance
  ext x
  simpa [Scheme.Hom.appLE] using map_eq_zero_iff _
    (ConcreteCategory.bijective_of_isIso
      (X.presheaf.map (eqToHom (image_morphismRestrict_preimage f U V)).op)).1

lemma ker_ideal_of_isPullback_of_isOpenImmersion {X Y U V : Scheme.{u}}
    (f : X ‚ü∂ Y) (f' : U ‚ü∂ V) (iU : U ‚ü∂ X) (iV : V ‚ü∂ Y) [IsOpenImmersion iV]
    [QuasiCompact f] (H : IsPullback f' iU iV f) (W) :
    f'.ker.ideal W =
      (f.ker.ideal ‚ü®iV ''·µÅ W, W.2.image_of_isOpenImmersion _‚ü©).comap (iV.appIso W).inv.hom := by
  have : QuasiCompact f' := MorphismProperty.of_isPullback H.flip inferInstance
  have : IsOpenImmersion iU := MorphismProperty.of_isPullback H inferInstance
  ext x
  have : iU ''·µÅ f' ‚Åª¬π·µÅ W = f ‚Åª¬π·µÅ iV ''·µÅ W :=
    IsOpenImmersion.image_preimage_eq_preimage_image_of_isPullback H W
  let e : Œì(X, f ‚Åª¬π·µÅ iV ''·µÅ W) ‚âÖ Œì(U, f' ‚Åª¬π·µÅ W) :=
    X.presheaf.mapIso (eqToIso this).op ‚â™‚â´ iU.appIso _
  have : (iV.appIso W).inv ‚â´ f.app _ = f'.app W ‚â´ e.inv := by
    rw [Iso.inv_comp_eq, ‚Üê Category.assoc, Iso.eq_comp_inv]
    simp only [Scheme.Hom.app_eq_appLE, Iso.trans_hom, Functor.mapIso_hom, Iso.op_hom, eqToIso.hom,
      eqToHom_op, Scheme.Hom.appIso_hom', Scheme.Hom.map_appLE, e, Scheme.appLE_comp_appLE, H.w]
  simp only [Scheme.Hom.ker_apply, RingHom.mem_ker, Ideal.mem_comap, ‚Üê RingHom.comp_apply,
    ‚Üê CommRingCat.hom_comp, Scheme.Hom.appIso_inv_app, this]
  simpa using (map_eq_zero_iff _ (ConcreteCategory.bijective_of_isIso e.inv).1).symm

lemma Hom.support_ker (f : X.Hom Y) [QuasiCompact f] :
    f.ker.support = closure (Set.range f.base) := by
  apply subset_antisymm
  ¬∑ wlog hY : ‚àÉ S, Y = Spec S
    ¬∑ intro x hx
      let ùí∞ := Y.affineCover
      obtain ‚ü®i, x, rfl‚ü© := ùí∞.exists_eq x
      have inst : QuasiCompact (ùí∞.pullbackHom f i) :=
        MorphismProperty.pullback_snd _ _ inferInstance
      have := this (ùí∞.pullbackHom f i) ‚ü®_, rfl‚ü©
        ((coe_support_inter _ ‚ü®‚ä§, isAffineOpen_top _‚ü©).ge ‚ü®?_, Set.mem_univ x‚ü©).1
      ¬∑ have := image_closure_subset_closure_image (f := (ùí∞.map i).base)
          (ùí∞.map i).base.1.2 (Set.mem_image_of_mem _ this)
        rw [‚Üê Set.range_comp, ‚Üê TopCat.coe_comp, ‚Üê Scheme.comp_base, ùí∞.pullbackHom_map] at this
        exact closure_mono (Set.range_comp_subset_range _ _) this
      ¬∑ rw [‚Üê (ùí∞.map i).isOpenEmbedding.injective.mem_set_image, Scheme.image_zeroLocus,
          ker_ideal_of_isPullback_of_isOpenImmersion f (ùí∞.pullbackHom f i)
            ((ùí∞.pullbackCover f).map i) (ùí∞.map i) (IsPullback.of_hasPullback _ _).flip,
          Ideal.coe_comap, Set.image_preimage_eq]
        ¬∑ exact ‚ü®((coe_support_inter _ _).le ‚ü®hx, by simp‚ü©).1, ‚ü®_, rfl‚ü©‚ü©
        ¬∑ exact (ConcreteCategory.bijective_of_isIso ((ùí∞.map i).appIso ‚ä§).inv).2
    obtain ‚ü®S, rfl‚ü© := hY
    wlog hX : ‚àÉ R, X = Spec R generalizing X S
    ¬∑ intro x hx
      have inst : CompactSpace X := HasAffineProperty.iff_of_isAffine.mp ‚ÄπQuasiCompact f‚Ä∫
      let ùí∞ := X.affineCover.finiteSubcover
      obtain ‚ü®_, ‚ü®i, rfl‚ü©, hx‚ü© := (f.iUnion_support_ker_openCover_map_comp ùí∞).ge hx
      have inst : QuasiCompact (ùí∞.map i ‚â´ f) := HasAffineProperty.iff_of_isAffine.mpr
        (by show CompactSpace (Spec _); infer_instance)
      exact closure_mono (Set.range_comp_subset_range _ _) (this S (ùí∞.map i ‚â´ f) ‚ü®_, rfl‚ü© hx)
    obtain ‚ü®R, rfl‚ü© := hX
    obtain ‚ü®œÜ, rfl‚ü© := Spec.map_surjective f
    rw [ker_of_isAffine, coe_support_ofIdealTop, Spec_zeroLocus, ‚Üê Ideal.coe_comap,
      RingHom.comap_ker, ‚Üê PrimeSpectrum.closure_range_comap, ‚Üê CommRingCat.hom_comp,
      ‚Üê Scheme.ŒìSpecIso_inv_naturality]
    simp only [CommRingCat.hom_comp, PrimeSpectrum.comap_comp, ContinuousMap.coe_comp]
    exact closure_mono (Set.range_comp_subset_range _ (Spec.map œÜ).base)
  ¬∑ rw [(support _).isClosed.closure_subset_iff]
    exact f.range_subset_ker_support

/-- The functor taking a morphism into `Y` to its kernel as an ideal sheaf on `Y`. -/
@[simps]
def kerFunctor (Y : Scheme.{u}) : (Over Y)·µí·µñ ‚•§ IdealSheafData Y where
  obj f := f.unop.hom.ker
  map {f g} hfg := homOfLE <| by simpa only [Functor.id_obj, Functor.const_obj_obj,
    OrderDual.toDual_le_toDual, ‚Üê Over.w hfg.unop] using hfg.unop.left.le_ker_comp f.unop.hom
  map_id _ := Subsingleton.elim _ _
  map_comp _ _ := Subsingleton.elim _ _

end ker

section subscheme
namespace IdealSheafData

open _root_.PrimeSpectrum Limits

variable (I : IdealSheafData X)

/-- `Spec (ùí™‚Çì(U)/I(U))`, the object to be glued into the closed subscheme. -/
def glueDataObj (U : X.affineOpens) : Scheme :=
  Spec (.of (Œì(X, U) ‚ß∏ I.ideal U))

/-- `Spec (ùí™‚Çì(U)/I(U)) ‚ü∂ Spec (ùí™‚Çì(U)) = U`, the closed immersion into `U`. -/
noncomputable
def glueDataObjŒπ (U : X.affineOpens) : I.glueDataObj U ‚ü∂ U.1 :=
  Spec.map (CommRingCat.ofHom (Ideal.Quotient.mk _)) ‚â´ U.2.isoSpec.inv

instance (U : X.affineOpens) : IsPreimmersion (I.glueDataObjŒπ U) :=
  have : IsPreimmersion (Spec.map (CommRingCat.ofHom (Ideal.Quotient.mk (I.ideal U)))) :=
    .mk_Spec_map
      (isClosedEmbedding_comap_of_surjective _ _ Ideal.Quotient.mk_surjective).isEmbedding
      (RingHom.surjectiveOnStalks_of_surjective Ideal.Quotient.mk_surjective)
  .comp _ _

lemma glueDataObjŒπ_Œπ (U : X.affineOpens) : I.glueDataObjŒπ U ‚â´ U.1.Œπ =
    Spec.map (CommRingCat.ofHom (Ideal.Quotient.mk _)) ‚â´ U.2.fromSpec := by
  rw [glueDataObjŒπ, Category.assoc]; rfl

lemma ker_glueDataObjŒπ_appTop (U : X.affineOpens) :
    RingHom.ker (I.glueDataObjŒπ U).appTop.hom = (I.ideal U).comap U.1.topIso.hom.hom := by
  let œÜ := CommRingCat.ofHom (Ideal.Quotient.mk (I.ideal U))
  rw [‚Üê Ideal.mk_ker (I := I.ideal _)]
  show RingHom.ker (Spec.map œÜ ‚â´ _).appTop.hom = (RingHom.ker œÜ.hom).comap _
  rw [‚Üê RingHom.ker_equiv_comp _ (Scheme.ŒìSpecIso _).commRingCatIsoToRingEquiv, RingHom.comap_ker,
    RingEquiv.toRingHom_eq_coe, Iso.commRingCatIsoToRingEquiv_toRingHom, ‚Üê CommRingCat.hom_comp,
    ‚Üê CommRingCat.hom_comp]
  congr 2
  simp only [Scheme.comp_app, TopologicalSpace.Opens.map_top, Category.assoc,
    Scheme.ŒìSpecIso_naturality, Scheme.Opens.topIso_hom]
  rw [‚Üê Scheme.Hom.appTop, U.2.isoSpec_inv_appTop, Category.assoc, Iso.inv_hom_id_assoc]
  simp only [Scheme.Opens.topIso_hom]

open scoped Set.Notation in
lemma range_glueDataObjŒπ (U : X.affineOpens) :
    Set.range (I.glueDataObjŒπ U).base =
      U.2.isoSpec.inv.base '' PrimeSpectrum.zeroLocus (I.ideal U) := by
  simp only [glueDataObjŒπ, Scheme.comp_coeBase, TopCat.coe_comp, Set.range_comp,
    SetLike.coe_sort_coe]
  erw [range_comap_of_surjective]
  swap; ¬∑ exact Ideal.Quotient.mk_surjective
  simp only [Ideal.mk_ker, CommRingCat.hom_ofHom]

lemma range_glueDataObjŒπ_Œπ (U : X.affineOpens) :
    Set.range ‚áë(I.glueDataObjŒπ U ‚â´ U.1.Œπ).base = X.zeroLocus (U := U) (I.ideal U) ‚à© U := by
  simp only [Scheme.comp_coeBase, TopCat.coe_comp, Set.range_comp, range_glueDataObjŒπ]
  rw [‚Üê Set.image_comp, ‚Üê TopCat.coe_comp, ‚Üê Scheme.comp_base, IsAffineOpen.isoSpec_inv_Œπ,
    IsAffineOpen.fromSpec_image_zeroLocus]

/-- The underlying space of `Spec (ùí™‚Çì(U)/I(U))` is homeomorphic to its image in `X`. -/
noncomputable
def glueDataObjCarrierIso (U : X.affineOpens) :
    (I.glueDataObj U).carrier ‚âÖ TopCat.of ‚Üë(X.zeroLocus (U := U) (I.ideal U) ‚à© U) :=
  TopCat.isoOfHomeo ((I.glueDataObjŒπ U ‚â´ U.1.Œπ).isEmbedding.toHomeomorph.trans
    (.setCongr (I.range_glueDataObjŒπ_Œπ U)))

/-- The open immersion `Spec Œì(ùí™‚Çì/I, U) ‚ü∂ Spec Œì(ùí™‚Çì/I, V)` if `U ‚â§ V`. -/
noncomputable
def glueDataObjMap {U V : X.affineOpens} (h : U ‚â§ V) : I.glueDataObj U ‚ü∂ I.glueDataObj V :=
  Spec.map (CommRingCat.ofHom (Ideal.quotientMap _ _ (I.ideal_le_comap_ideal h)))

lemma isLocalization_away {U V : X.affineOpens}
    (h : U ‚â§ V) (f : Œì(X, V.1)) (hU : U = X.affineBasicOpen f) :
      letI := (Ideal.quotientMap _ _ (I.ideal_le_comap_ideal h)).toAlgebra
      IsLocalization.Away (Ideal.Quotient.mk (I.ideal V) f) (Œì(X, U) ‚ß∏ (I.ideal U)) := by
  letI := (Ideal.quotientMap _ _ (I.ideal_le_comap_ideal h)).toAlgebra
  let f' : Œì(X, V) ‚ß∏ I.ideal V := Ideal.Quotient.mk _ f
  letI := (X.presheaf.map (homOfLE (X := X.Opens) h).op).hom.toAlgebra
  have : IsLocalization.Away f Œì(X, U) := by
    subst hU; exact V.2.isLocalization_of_eq_basicOpen _ _ rfl
  simp only [IsLocalization.Away, f', ‚Üê Submonoid.map_powers]
  refine IsLocalization.of_surjective _ _ _ Ideal.Quotient.mk_surjective _
    Ideal.Quotient.mk_surjective ?_ ?_
  ¬∑ simp [RingHom.algebraMap_toAlgebra, Ideal.quotientMap_comp_mk]; rfl
  ¬∑ subst hU
    simp only [Ideal.mk_ker, RingHom.algebraMap_toAlgebra, I.map_ideal', le_refl]

instance isOpenImmersion_glueDataObjMap {V : X.affineOpens} (f : Œì(X, V.1)) :
    IsOpenImmersion (I.glueDataObjMap (X.affineBasicOpen_le f)) := by
  letI := (Ideal.quotientMap _ _ (I.ideal_le_comap_ideal (X.affineBasicOpen_le f))).toAlgebra
  have := I.isLocalization_away (X.affineBasicOpen_le f) f rfl
  exact IsOpenImmersion.of_isLocalization (Ideal.Quotient.mk _ f)

lemma opensRange_glueDataObjMap {V : X.affineOpens} (f : Œì(X, V.1)) :
      (I.glueDataObjMap (X.affineBasicOpen_le f)).opensRange =
        (I.glueDataObjŒπ V) ‚Åª¬π·µÅ (V.1.Œπ ‚Åª¬π·µÅ X.basicOpen f) := by
  letI := (Ideal.quotientMap _ _ (I.ideal_le_comap_ideal (X.affineBasicOpen_le f))).toAlgebra
  let f' : Œì(X, V) ‚ß∏ I.ideal V := Ideal.Quotient.mk _ f
  have := I.isLocalization_away (X.affineBasicOpen_le f) f rfl
  ext1
  refine (localization_away_comap_range _ f').trans ?_
  rw [‚Üê comap_basicOpen, ‚Üê V.2.fromSpec_preimage_basicOpen, ‚Üê Scheme.preimage_comp, glueDataObjŒπ_Œπ]
  rfl

@[reassoc (attr := simp)]
lemma glueDataObjMap_glueDataObjŒπ {U V : X.affineOpens} (h : U ‚â§ V) :
    I.glueDataObjMap h ‚â´ I.glueDataObjŒπ V = I.glueDataObjŒπ U ‚â´ X.homOfLE h := by
  rw [glueDataObjMap, glueDataObjŒπ, ‚Üê Spec.map_comp_assoc, ‚Üê CommRingCat.ofHom_comp,
    Ideal.quotientMap_comp_mk, CommRingCat.ofHom_comp, Spec.map_comp_assoc, glueDataObjŒπ,
    Category.assoc]
  congr 1
  rw [Iso.eq_inv_comp, IsAffineOpen.isoSpec_hom, CommRingCat.ofHom_hom]
  erw [Scheme.Opens.toSpecŒì_SpecMap_map_assoc U.1 V.1 h]
  rw [‚Üê IsAffineOpen.isoSpec_hom, Iso.hom_inv_id, Category.comp_id]

lemma ideal_le_ker_glueDataObjŒπ (U V : X.affineOpens) :
    I.ideal V ‚â§ RingHom.ker (U.1.Œπ.app V.1 ‚â´ (I.glueDataObjŒπ U).app _).hom := by
  intro x hx
  apply (I.glueDataObj U).IsSheaf.section_ext
  intro p hp
  obtain ‚ü®f, g, hfg, hf‚ü© := exists_basicOpen_le_affine_inter U.2 V.2 ((I.glueDataObjŒπ U).base p).1
      ‚ü®((I.glueDataObjŒπ U).base p).2, hp‚ü©
  refine ‚ü®(I.glueDataObjŒπ U ‚Åª¬π·µÅ U.1.Œπ ‚Åª¬π·µÅ X.basicOpen f),
    fun x hx ‚Ü¶ X.basicOpen_le g (hfg ‚ñ∏ hx), hf, ?_‚ü©
  have := Hom.isIso_app (I.glueDataObjMap (X.affineBasicOpen_le f))
    (I.glueDataObjŒπ U ‚Åª¬π·µÅ U.1.Œπ ‚Åª¬π·µÅ X.basicOpen f) (by rw [opensRange_glueDataObjMap])
  apply ((ConcreteCategory.isIso_iff_bijective _).mp this).1
  simp only [CommRingCat.forget_map, map_zero, ‚Üê RingHom.comp_apply,
    ‚Üê CommRingCat.hom_comp, Category.assoc]
  simp only [Scheme.Hom.app_eq_appLE, homOfLE_leOfHom, Scheme.Hom.map_appLE,
    Scheme.appLE_comp_appLE, Category.assoc, glueDataObjMap_glueDataObjŒπ_assoc]
  rw [Scheme.Hom.appLE]
  have H : (X.homOfLE (X.basicOpen_le f) ‚â´ U.1.Œπ) ‚Åª¬π·µÅ V.1 = ‚ä§ := by
    simp only [Scheme.homOfLE_Œπ, ‚Üê top_le_iff]
    exact fun x _ ‚Ü¶ (hfg.trans_le (X.basicOpen_le g)) x.2
  simp only [Scheme.comp_app, Scheme.Opens.Œπ_app, Scheme.homOfLE_app, ‚Üê Functor.map_comp,
    Scheme.app_eq _ H, Scheme.Opens.toScheme_presheaf_map, ‚Üê Functor.map_comp_assoc, Category.assoc]
  simp only [CommRingCat.hom_comp, RingHom.comp_apply]
  convert RingHom.map_zero _ using 2
  rw [‚Üê RingHom.mem_ker, ker_glueDataObjŒπ_appTop, ‚Üê Ideal.mem_comap, Ideal.comap_comap,
    ‚Üê CommRingCat.hom_comp]
  simp only [Scheme.affineBasicOpen_coe, homOfLE_leOfHom, Scheme.comp_coeBase,
    TopologicalSpace.Opens.map_comp_obj, eqToHom_op, eqToHom_unop, ‚Üê Functor.map_comp,
    Scheme.Opens.topIso_hom, Category.assoc, ‚Üê CommRingCat.hom_comp]
  exact I.ideal_le_comap_ideal (U := X.affineBasicOpen f) (V := V)
    (hfg.trans_le (X.basicOpen_le g)) hx

/-- (Implementation) The intersections `Spec Œì(ùí™‚Çì/I, U) ‚à© V` useful for gluing. -/
private noncomputable
abbrev glueDataObjPullback (U V : X.affineOpens) : Scheme :=
  pullback (I.glueDataObjŒπ U) (X.homOfLE (U := U.1 ‚äì V.1) inf_le_left)

/-- (Implementation) Transition maps in the glue data for `ùí™‚Çì/I`. -/
private noncomputable
def glueDataT (U V : X.affineOpens) :
    I.glueDataObjPullback U V ‚ü∂ I.glueDataObjPullback V U := by
  letI F := pullback.snd (I.glueDataObjŒπ U) (X.homOfLE (inf_le_left (b := V.1)))
  refine pullback.lift ((F ‚â´ X.homOfLE inf_le_right ‚â´
    V.2.isoSpec.hom).liftQuotient _ ?_) (F ‚â´ X.homOfLE (by simp)) ?_
  ¬∑ intro x hx
    simp only [comp_app, comp_coeBase, TopologicalSpace.Opens.map_comp_obj,
      TopologicalSpace.Opens.map_top, homOfLE_app, homOfLE_leOfHom, Category.assoc,
      ‚Üê CommRingCat.hom_comp, RingHom.mem_ker, ‚Üê RingHom.comp_apply, Function.comp_apply]
    convert_to (U.1.Œπ.app V.1 ‚â´ (F ‚â´ X.homOfLE inf_le_left).appLE (U.1.Œπ ‚Åª¬π·µÅ V.1) ‚ä§
      (by rw [‚Üê Scheme.preimage_comp, Category.assoc, X.homOfLE_Œπ]
          exact fun x _ ‚Ü¶ by simpa using (F.base x).2.2)).hom x = 0 using 3
    ¬∑ simp only [homOfLE_leOfHom, Opens.Œπ_app, comp_appLE, homOfLE_app]
      have H : ‚ä§ ‚â§ X.homOfLE (inf_le_left (b := V.1)) ‚Åª¬π·µÅ U.1.Œπ ‚Åª¬π·µÅ V.1 := by
        rw [‚Üê Scheme.preimage_comp, X.homOfLE_Œπ]; exact fun x _ ‚Ü¶ by simpa using x.2.2
      rw [‚Üê F.map_appLE (show ‚ä§ ‚â§ F ‚Åª¬π·µÅ ‚ä§ from le_rfl) (homOfLE H).op]
      simp only [homOfLE_leOfHom, Opens.toScheme_presheaf_map, Quiver.Hom.unop_op,
        Hom.opensFunctor_map_homOfLE, ‚Üê Functor.map_comp_assoc, IsAffineOpen.isoSpec_hom_appTop,
        Opens.topIso_inv, eqToHom_op, homOfLE_leOfHom, Category.assoc,
        Iso.inv_hom_id_assoc, F.app_eq_appLE]
      rfl
    ¬∑ have : (U.1.Œπ.app V.1 ‚â´ (I.glueDataObjŒπ U).app (U.1.Œπ ‚Åª¬π·µÅ V.1)).hom x = 0 :=
        I.ideal_le_ker_glueDataObjŒπ U V hx
      simp_rw [F, ‚Üê pullback.condition]
      simp only [Scheme.Opens.Œπ_app, CommRingCat.hom_comp, RingHom.coe_comp,
        Function.comp_apply, Scheme.Hom.appLE, Scheme.comp_app, Category.assoc, F] at this ‚ä¢
      simp only [this, map_zero]
  ¬∑ conv_lhs => enter [2]; rw [glueDataObjŒπ]
    rw [Scheme.Hom.liftQuotient_comp_assoc, Category.assoc, Category.assoc, Iso.hom_inv_id,
      Category.comp_id, Category.assoc, X.homOfLE_homOfLE]

@[reassoc (attr := simp)]
private lemma glueDataT_snd (U V : X.affineOpens) :
    I.glueDataT U V ‚â´ pullback.snd _ _ = pullback.snd _ _ ‚â´ X.homOfLE (by simp) :=
  pullback.lift_snd _ _ _

@[reassoc (attr := simp)]
private lemma glueDataT_fst (U V : X.affineOpens) :
    I.glueDataT U V ‚â´ pullback.fst _ _ ‚â´ glueDataObjŒπ _ _ =
      pullback.snd _ _ ‚â´ X.homOfLE inf_le_right := by
  refine (pullback.lift_fst_assoc _ _ _ _).trans ?_
  conv_lhs => enter [2]; rw [glueDataObjŒπ]
  rw [Scheme.Hom.liftQuotient_comp_assoc, Category.assoc, Category.assoc, Iso.hom_inv_id,
    Category.comp_id]

/-- (Implementation) `t'` in the glue data for `ùí™‚Çì/I`. -/
private noncomputable
def glueDataT'Aux (U V W U‚ÇÄ : X.affineOpens) (hU‚ÇÄ : U.1 ‚äì W ‚â§ U‚ÇÄ) :
    pullback
      (pullback.fst _ _ : I.glueDataObjPullback U V ‚ü∂ _)
      (pullback.fst _ _ : I.glueDataObjPullback U W ‚ü∂ _) ‚ü∂ I.glueDataObjPullback V U‚ÇÄ :=
  pullback.lift
    (pullback.fst _ _ ‚â´ I.glueDataT U V ‚â´ pullback.fst _ _)
    (IsOpenImmersion.lift (V.1 ‚äì U‚ÇÄ.1).Œπ
      (pullback.fst _ _ ‚â´ pullback.fst _ _ ‚â´ I.glueDataObjŒπ U ‚â´ U.1.Œπ) (by
      simp only [Scheme.Opens.range_Œπ, TopologicalSpace.Opens.coe_inf, Set.subset_inter_iff]
      constructor
      ¬∑ rw [pullback.condition_assoc (f := I.glueDataObjŒπ U), X.homOfLE_Œπ,
          ‚Üê Category.assoc, Scheme.comp_coeBase, TopCat.coe_comp]
        exact (Set.range_comp_subset_range _ _).trans (by simp)
      ¬∑ rw [pullback.condition_assoc, pullback.condition_assoc, X.homOfLE_Œπ,
          ‚Üê Category.assoc, Scheme.comp_coeBase, TopCat.coe_comp]
        exact (Set.range_comp_subset_range _ _).trans (by simpa using hU‚ÇÄ))) (by
      rw [‚Üê cancel_mono (Scheme.Opens.Œπ _)]
      simp [pullback.condition_assoc])

@[reassoc (attr := simp)]
private lemma glueDataT'Aux_fst (U V W U‚ÇÄ : X.affineOpens) (hU‚ÇÄ : U.1 ‚äì W ‚â§ U‚ÇÄ) :
    I.glueDataT'Aux U V W U‚ÇÄ hU‚ÇÄ ‚â´ pullback.fst _ _ =
      pullback.fst _ _ ‚â´ I.glueDataT U V ‚â´ pullback.fst _ _ := pullback.lift_fst _ _ _

@[reassoc (attr := simp)]
private lemma glueDataT'Aux_snd_Œπ (U V W U‚ÇÄ : X.affineOpens) (hU‚ÇÄ : U.1 ‚äì W ‚â§ U‚ÇÄ) :
    I.glueDataT'Aux U V W U‚ÇÄ hU‚ÇÄ ‚â´ pullback.snd _ _ ‚â´ (V.1 ‚äì U‚ÇÄ.1).Œπ =
      pullback.fst _ _ ‚â´ pullback.fst _ _ ‚â´ I.glueDataObjŒπ U ‚â´ U.1.Œπ :=
  (pullback.lift_snd_assoc _ _ _ _).trans (IsOpenImmersion.lift_fac _ _ _)

/-- (Implementation) The glue data for `ùí™‚Çì/I`. -/
@[simps]
private noncomputable
def glueData : Scheme.GlueData where
  J := X.affineOpens
  U := I.glueDataObj
  V ij := I.glueDataObjPullback ij.1 ij.2
  f i j := pullback.fst _ _
  f_id i :=
    have : IsIso (X.homOfLE (inf_le_left (a := i.1) (b := i.1))) :=
      ‚ü®X.homOfLE (by simp), by simp, by simp‚ü©
    inferInstance
  t i j := I.glueDataT i j
  t_id i := by
    apply pullback.hom_ext
    ¬∑ rw [‚Üê cancel_mono (glueDataObjŒπ _ _)]
      simp [pullback.condition]
    ¬∑ simp
  t' i j k := pullback.lift
    (I.glueDataT'Aux _ _ _ _ inf_le_right) (I.glueDataT'Aux _ _ _ _ inf_le_left) (by simp)
  t_fac i j k := by
    apply pullback.hom_ext
    ¬∑ rw [‚Üê cancel_mono (glueDataObjŒπ _ _)]
      simp
    ¬∑ rw [‚Üê cancel_mono (Scheme.Opens.Œπ _)]
      simp [pullback.condition_assoc]
  cocycle i j k := by
    dsimp only
    apply pullback.hom_ext
    ¬∑ apply pullback.hom_ext
      ¬∑ rw [‚Üê cancel_mono (glueDataObjŒπ _ _), ‚Üê cancel_mono (Scheme.Opens.Œπ _)]
        simp only [Category.assoc, limit.lift_œÄ, PullbackCone.mk_pt, PullbackCone.mk_œÄ_app,
          glueDataT'Aux_fst, limit.lift_œÄ_assoc, cospan_left, glueDataT_fst, Scheme.homOfLE_Œπ,
          glueDataT'Aux_snd_Œπ, glueDataT'Aux_fst_assoc, glueDataT_fst_assoc, Category.id_comp]
        rw [pullback.condition_assoc (f := I.glueDataObjŒπ i)]
        simp
      ¬∑ rw [‚Üê cancel_mono (Scheme.Opens.Œπ _)]
        simp [pullback.condition_assoc]
    ¬∑ apply pullback.hom_ext
      ¬∑ rw [‚Üê cancel_mono (glueDataObjŒπ _ _), ‚Üê cancel_mono (Scheme.Opens.Œπ _)]
        simp only [Category.assoc, limit.lift_œÄ, PullbackCone.mk_pt, PullbackCone.mk_œÄ_app,
          glueDataT'Aux_fst, limit.lift_œÄ_assoc, cospan_left, glueDataT_fst, Scheme.homOfLE_Œπ,
          glueDataT'Aux_snd_Œπ, glueDataT'Aux_fst_assoc, glueDataT_fst_assoc, Category.id_comp]
        rw [‚Üê pullback.condition_assoc, pullback.condition_assoc (f := I.glueDataObjŒπ i),
          X.homOfLE_Œπ]
      ¬∑ rw [‚Üê cancel_mono (Scheme.Opens.Œπ _)]
        simp only [Category.assoc, limit.lift_œÄ, PullbackCone.mk_pt, PullbackCone.mk_œÄ_app,
          glueDataT'Aux_snd_Œπ, limit.lift_œÄ_assoc, cospan_left, glueDataT'Aux_fst_assoc,
          glueDataT_fst_assoc, Scheme.homOfLE_Œπ, Category.id_comp]
        rw [pullback.condition_assoc, pullback.condition_assoc, X.homOfLE_Œπ]
  f_open i j := inferInstance

/-- (Implementation) The map from `Spec(ùí™‚Çì/I)` to `X`. See `IdealSheafData.subschemeŒπ` instead. -/
private noncomputable
def gluedTo : I.glueData.glued ‚ü∂ X :=
  Multicoequalizer.desc _ _ (fun i ‚Ü¶ I.glueDataObjŒπ i ‚â´ i.1.Œπ)
    (by simp [GlueData.diagram, pullback.condition_assoc])

@[reassoc (attr := simp)]
private lemma Œπ_gluedTo (U : X.affineOpens) :
    I.glueData.Œπ U ‚â´ I.gluedTo = I.glueDataObjŒπ U ‚â´ U.1.Œπ :=
  Multicoequalizer.œÄ_desc _ _ _ _ _

@[reassoc (attr := simp)]
private lemma glueDataObjMap_Œπ (U V : X.affineOpens) (h : U ‚â§ V) :
    I.glueDataObjMap h ‚â´ I.glueData.Œπ V = I.glueData.Œπ U := by
  have : IsIso (X.homOfLE inf_le_left : (U.1 ‚äì V.1).toScheme ‚ü∂ U) :=
    ‚ü®X.homOfLE (by simpa), by simp, by simp‚ü©
  have H : inv (X.homOfLE inf_le_left : (U.1 ‚äì V.1).toScheme ‚ü∂ U) = X.homOfLE (by simpa) := by
    rw [eq_comm, ‚Üê hom_comp_eq_id]; simp
  have := I.glueData.glue_condition U V
  simp only [glueData_J, glueData_V, glueData_t, glueData_U, glueData_f] at this
  rw [‚Üê IsIso.inv_comp_eq] at this
  rw [‚Üê Category.id_comp (I.glueData.Œπ U), ‚Üê this]
  simp_rw [‚Üê Category.assoc]
  congr 1
  rw [‚Üê cancel_mono (glueDataObjŒπ _ _)]
  simp [pullback_inv_fst_snd_of_right_isIso_assoc, H]

private lemma gluedTo_injective :
    Function.Injective I.gluedTo.base := by
  intro a b e
  obtain ‚ü®ia, a : I.glueDataObj ia, rfl‚ü© :=
    I.glueData.toGlueData.Œπ_jointly_surjective (Scheme.forgetToTop ‚ãô forget _) a
  obtain ‚ü®ib, b : I.glueDataObj ib, rfl‚ü© :=
    I.glueData.toGlueData.Œπ_jointly_surjective (Scheme.forgetToTop ‚ãô forget _) b
  show (I.glueData.Œπ ia).base a = (I.glueData.Œπ ib).base b
  have : ((I.glueDataObjŒπ ia).base a).1 = ((I.glueDataObjŒπ ib).base b).1 := by
    have : (I.glueData.Œπ ia ‚â´ I.gluedTo).base a =
      (I.glueData.Œπ ib ‚â´ I.gluedTo).base b := e
    rwa [Œπ_gluedTo, Œπ_gluedTo] at this
  obtain ‚ü®f, g, hfg, H‚ü© := exists_basicOpen_le_affine_inter ia.2 ib.2
    ((I.glueDataObjŒπ ia).base a).1
      ‚ü®((I.glueDataObjŒπ ia).base a).2, this ‚ñ∏ ((I.glueDataObjŒπ ib).base b).2‚ü©
  have hmem (W) (hW : W = X.affineBasicOpen g) :
      b ‚àà Set.range (I.glueDataObjMap (hW.trans_le (X.affineBasicOpen_le g))).base := by
    subst hW
    refine (I.opensRange_glueDataObjMap g).ge ?_
    show ((I.glueDataObjŒπ ib).base b).1 ‚àà X.basicOpen g
    rwa [‚Üê this, ‚Üê hfg]
  obtain ‚ü®a, rfl‚ü© := (I.opensRange_glueDataObjMap f).ge H
  obtain ‚ü®b, rfl‚ü© := hmem (X.affineBasicOpen f) (Subtype.ext hfg)
  simp only [glueData_U, ‚Üê Scheme.comp_base_apply, glueDataObjMap_glueDataObjŒπ] at this ‚ä¢
  simp only [Scheme.affineBasicOpen_coe, Scheme.comp_coeBase, TopCat.comp_app,
    Scheme.homOfLE_apply, SetLike.coe_eq_coe] at this
  obtain rfl := (I.glueDataObjŒπ (X.affineBasicOpen f)).isEmbedding.injective this
  simp only [glueDataObjMap_Œπ]

lemma range_glueDataObjŒπ_Œπ_eq_support_inter (U : X.affineOpens) :
    Set.range (I.glueDataObjŒπ U ‚â´ U.1.Œπ).base = (I.support : Set X) ‚à© U :=
  (I.range_glueDataObjŒπ_Œπ U).trans (I.coe_support_inter U).symm

private lemma range_gluedTo :
    Set.range I.gluedTo.base = I.support := by
  refine subset_antisymm (Set.range_subset_iff.mpr fun x ‚Ü¶ ?_) ?_
  ¬∑ obtain ‚ü®ix, x : I.glueDataObj ix, rfl‚ü© :=
      I.glueData.toGlueData.Œπ_jointly_surjective (Scheme.forgetToTop ‚ãô forget _) x
    show (I.glueData.Œπ _ ‚â´ I.gluedTo).base x ‚àà I.support
    rw [Œπ_gluedTo]
    exact ((I.range_glueDataObjŒπ_Œπ_eq_support_inter ix).le ‚ü®_, rfl‚ü©).1
  ¬∑ intro x hx
    obtain ‚ü®_, ‚ü®U, hU, rfl‚ü©, hxU, -‚ü© :=
      (isBasis_affine_open X).exists_subset_of_mem_open (Set.mem_univ x) isOpen_univ
    obtain ‚ü®y, rfl‚ü© := (I.range_glueDataObjŒπ_Œπ_eq_support_inter ‚ü®U, hU‚ü©).ge ‚ü®hx, hxU‚ü©
    rw [‚Üê Œπ_gluedTo]
    exact ‚ü®_, rfl‚ü©

private lemma range_glueData_Œπ (U : X.affineOpens) :
    Set.range (Scheme.Hom.toLRSHom' (X := I.glueDataObj U) <|
      I.glueData.Œπ U).base = (I.gluedTo ‚Åª¬π·µÅ U : Set I.glueData.glued) := by
  simp only [Scheme.Opens.range_Œπ, TopologicalSpace.Opens.map_coe, glueData_U]
  apply I.gluedTo_injective.image_injective
  rw [‚Üê Set.range_comp, ‚Üê TopCat.coe_comp, ‚Üê Scheme.comp_base, Œπ_gluedTo,
    range_glueDataObjŒπ_Œπ, Set.image_preimage_eq_inter_range, range_gluedTo,
    ‚Üê coe_support_inter, Set.inter_comm]

/-- (Implementation) identifying `Spec(Œì(X, U)/I(U))` with its image in `Spec(ùí™‚Çì/I)`. -/
private noncomputable
def glueDataObjIso (U : X.affineOpens) :
    I.glueDataObj U ‚âÖ I.gluedTo ‚Åª¬π·µÅ U :=
  IsOpenImmersion.isoOfRangeEq (I.glueData.Œπ U) (Scheme.Opens.Œπ _) (by
    simp only [Scheme.Opens.range_Œπ, TopologicalSpace.Opens.map_coe, glueData_U, range_glueData_Œπ])

@[reassoc (attr := simp)]
private lemma glueDataObjIso_hom_Œπ (U : X.affineOpens) :
    (I.glueDataObjIso U).hom ‚â´ (I.gluedTo ‚Åª¬π·µÅ U).Œπ = I.glueData.Œπ U :=
  IsOpenImmersion.isoOfRangeEq_hom_fac _ _ _

private lemma glueDataObjIso_hom_restrict (U : X.affineOpens) :
    (I.glueDataObjIso U).hom ‚â´ I.gluedTo ‚à£_ ‚ÜëU = I.glueDataObjŒπ U := by
  rw [‚Üê cancel_mono U.1.Œπ]; simp

private instance : IsPreimmersion I.gluedTo := by
  rw [IsLocalAtTarget.iff_of_iSup_eq_top (P := @IsPreimmersion) _ (iSup_affineOpens_eq_top X)]
  intro U
  rw [‚Üê MorphismProperty.cancel_left_of_respectsIso @IsPreimmersion (I.glueDataObjIso U).hom,
    glueDataObjIso_hom_restrict]
  infer_instance

private instance : QuasiCompact I.gluedTo :=
  ‚ü®fun _ _ ‚Ü¶ (Topology.IsClosedEmbedding.isProperMap
    ‚ü®I.gluedTo.isEmbedding, I.range_gluedTo ‚ñ∏ I.support.isClosed‚ü©).isCompact_preimage‚ü©

/-- (Implementation) The underlying space of `Spec(ùí™‚Çì/I)` is homeomorphic to the support of `I`. -/
private noncomputable
def gluedHomeo : I.glueData.glued ‚âÉ‚Çú I.support :=
  I.gluedTo.isEmbedding.toHomeomorph.trans (.setCongr I.range_gluedTo)

/-- The subscheme associated to an ideal sheaf. -/
noncomputable
def subscheme : Scheme :=
  I.glueData.glued.restrict
    (f := TopCat.ofHom (toContinuousMap I.gluedHomeo.symm))
    I.gluedHomeo.symm.isOpenEmbedding

/-- (Implementation) The isomorphism between the subscheme and the glued scheme. -/
private noncomputable
def subschemeIso : I.subscheme ‚âÖ I.glueData.glued :=
  letI F := I.glueData.glued.ofRestrict (f := TopCat.ofHom (toContinuousMap I.gluedHomeo.symm))
    I.gluedHomeo.symm.isOpenEmbedding
  have : Epi F.base := ConcreteCategory.epi_of_surjective _ I.gluedHomeo.symm.surjective
  letI := IsOpenImmersion.to_iso F
  asIso F

/-- The inclusion from the subscheme associated to an ideal sheaf. -/
noncomputable
def subschemeŒπ : I.subscheme ‚ü∂ X :=
    (I.subschemeIso.hom ‚â´ I.gluedTo).copyBase Subtype.val <| by
  ext x
  show (I.gluedHomeo (I.gluedHomeo.symm x)).1 = x.1
  rw [I.gluedHomeo.apply_symm_apply]

lemma subschemeŒπ_apply (x : I.subscheme) : I.subschemeŒπ.base x = x.1 := rfl

private lemma subschemeŒπ_def : I.subschemeŒπ = I.subschemeIso.hom ‚â´ I.gluedTo :=
  Scheme.Hom.copyBase_eq _ _ _

/-- See `AlgebraicGeometry.Morphisms.ClosedImmersion` for the closed immersion version. -/
instance : IsPreimmersion I.subschemeŒπ := by
  rw [subschemeŒπ_def]
  infer_instance

instance : QuasiCompact I.subschemeŒπ := by
  rw [subschemeŒπ_def]
  infer_instance

@[simp]
lemma range_subschemeŒπ : Set.range I.subschemeŒπ.base = I.support := by
  simp [‚Üê range_gluedTo, I.subschemeŒπ_def, Set.range_comp,
    Set.range_eq_univ.mpr I.subschemeIso.hom.homeomorph.surjective]

private lemma opensRange_glueData_Œπ_subschemeIso_inv (U : X.affineOpens) :
    (I.glueData.Œπ U ‚â´ I.subschemeIso.inv).opensRange = I.subschemeŒπ ‚Åª¬π·µÅ U := by
  ext1
  simp [Set.range_comp, I.range_glueData_Œπ, subschemeŒπ_def, Set.preimage_comp,
    ‚Üê coe_homeoOfIso_symm, ‚Üê homeoOfIso_symm, ‚Üê Homeomorph.coe_symm_toEquiv,
    ‚Üê Set.preimage_equiv_eq_image_symm]

/-- The subscheme associated to an ideal sheaf `I` is covered by `Spec(Œì(X, U)/I(U))`. -/
noncomputable
def subschemeCover : I.subscheme.AffineOpenCover where
  J := X.affineOpens
  obj U := .of <| Œì(X, U) ‚ß∏ I.ideal U
  map U := I.glueData.Œπ U ‚â´ I.subschemeIso.inv
  f x := (X.openCoverOfISupEqTop _ (iSup_affineOpens_eq_top X)).f x.1
  covers x := by
    let U := (X.openCoverOfISupEqTop _ (iSup_affineOpens_eq_top X)).f x.1
    obtain ‚ü®‚ü®y, hy : y ‚àà U.1‚ü©, rfl : y = x.1‚ü© :=
      (X.openCoverOfISupEqTop _ (iSup_affineOpens_eq_top X)).covers x.1
    exact (I.opensRange_glueData_Œπ_subschemeIso_inv U).ge hy

@[simp]
lemma opensRange_subschemeCover_map (U : X.affineOpens) :
    (I.subschemeCover.map U).opensRange = I.subschemeŒπ ‚Åª¬π·µÅ U :=
  I.opensRange_glueData_Œπ_subschemeIso_inv U

@[simp]
lemma subschemeCover_map_subschemeŒπ (U : X.affineOpens) :
    I.subschemeCover.map U ‚â´ I.subschemeŒπ = I.glueDataObjŒπ U ‚â´ U.1.Œπ := by
  simp [subschemeCover, subschemeŒπ_def]

/-- `Œì(ùí™‚Çì/I, U) ‚âÖ ùí™‚Çì(U)/I(U)`. -/
noncomputable
def subschemeObjIso (U : X.affineOpens) :
    Œì(I.subscheme, I.subschemeŒπ ‚Åª¬π·µÅ U) ‚âÖ .of (Œì(X, U) ‚ß∏ I.ideal U) :=
  I.subscheme.presheaf.mapIso (eqToIso (by simp)).op ‚â™‚â´
    (I.subschemeCover.map U).appIso _ ‚â™‚â´ Scheme.ŒìSpecIso (.of (Œì(X, U) ‚ß∏ I.ideal U))

lemma subschemeŒπ_app (U : X.affineOpens) : I.subschemeŒπ.app U =
    CommRingCat.ofHom (Ideal.Quotient.mk (I.ideal U)) ‚â´
    (I.subschemeObjIso U).inv := by
  have := I.subschemeCover_map_subschemeŒπ U
  simp only [glueDataObjŒπ, Category.assoc, IsAffineOpen.isoSpec_inv_Œπ] at this
  replace this := Scheme.congr_app this U
  simp only [comp_coeBase, TopologicalSpace.Opens.map_comp_obj, comp_app,
    IsAffineOpen.fromSpec_app_self, eqToHom_op, Category.assoc, Hom.naturality_assoc,
    TopologicalSpace.Opens.map_top, ‚Üê ŒìSpecIso_inv_naturality_assoc] at this
  simp_rw [‚Üê Category.assoc, ‚Üê IsIso.comp_inv_eq] at this
  simp only [‚Üê this, ‚Üê Functor.map_inv, inv_eqToHom, Category.assoc, eqToHom_unop,
    ‚Üê Functor.map_comp, IsIso.Iso.inv_inv, subschemeObjIso, Iso.trans_inv, Functor.mapIso_inv,
    Iso.op_inv, eqToIso.inv, eqToHom_op, Iso.hom_inv_id_assoc, Hom.appIso_inv_naturality_assoc,
    Functor.op_obj, Functor.op_map, unop_comp, unop_inv, Quiver.Hom.unop_op,
    Hom.app_appIso_inv_assoc, TopologicalSpace.Opens.carrier_eq_coe, TopologicalSpace.Opens.map_coe,
    homOfLE_leOfHom]
  convert (Category.comp_id _).symm
  exact CategoryTheory.Functor.map_id _ _

lemma subschemeŒπ_app_surjective (U : X.affineOpens) :
    Function.Surjective (I.subschemeŒπ.app U) := by
  rw [I.subschemeŒπ_app U]
  exact (I.subschemeObjIso U).commRingCatIsoToRingEquiv.symm.surjective.comp
    Ideal.Quotient.mk_surjective

lemma ker_subschemeŒπ_app (U : X.affineOpens) :
    RingHom.ker (I.subschemeŒπ.app U).hom = I.ideal U := by
  rw [subschemeŒπ_app]
  let e : CommRingCat.of (Œì(X, U) ‚ß∏ I.ideal U) ‚âÖ Œì(I.subscheme, I.subschemeŒπ ‚Åª¬π·µÅ U) :=
    (Scheme.ŒìSpecIso _).symm ‚â™‚â´ ((I.subschemeCover.map U).appIso _).symm ‚â™‚â´
      I.subscheme.presheaf.mapIso (eqToIso (by simp)).op
  show RingHom.ker (e.commRingCatIsoToRingEquiv.toRingHom.comp
    (Ideal.Quotient.mk (I.ideal U))) = _
  rw [RingHom.ker_equiv_comp, Ideal.mk_ker]

@[simp]
lemma ker_subschemeŒπ : I.subschemeŒπ.ker = I := by
  ext; simp [ker_subschemeŒπ_app]

/-- Given `I ‚â§ J`, this is the map `Spec(Œì(X, U)/J(U)) ‚ü∂ Spec(Œì(X, U)/I(U))`. -/
noncomputable
def glueDataObjHom {I J : IdealSheafData X} (h : I ‚â§ J) (U) :
    J.glueDataObj U ‚ü∂ I.glueDataObj U :=
  Spec.map (CommRingCat.ofHom (Ideal.Quotient.factor (h U)))

@[reassoc (attr := simp)]
lemma glueDataObjHom_Œπ {I J : IdealSheafData X} (h : I ‚â§ J) (U) :
    glueDataObjHom h U ‚â´ I.glueDataObjŒπ U = J.glueDataObjŒπ U := by
  rw [glueDataObjHom, glueDataObjŒπ, glueDataObjŒπ, ‚Üê Spec.map_comp_assoc, ‚Üê CommRingCat.ofHom_comp,
    Ideal.Quotient.factor_comp_mk]

@[simp]
lemma glueDataObjHom_id {I : IdealSheafData X} (U) :
    glueDataObjHom (le_refl I) U = ùüô _ := by
  rw [‚Üê cancel_mono (I.glueDataObjŒπ U)]
  simp

@[reassoc (attr := simp)]
lemma glueDataObjHom_comp {I J K : IdealSheafData X} (hIJ : I ‚â§ J) (hJK : J ‚â§ K) (U) :
    glueDataObjHom hJK U ‚â´ glueDataObjHom hIJ U = glueDataObjHom (hIJ.trans hJK) U := by
  rw [‚Üê cancel_mono (I.glueDataObjŒπ U)]
  simp

/-- The inclusion of ideal sheaf induces an inclusion of subschemes -/
noncomputable
def inclusion {I J : IdealSheafData X} (h : I ‚â§ J) :
    J.subscheme ‚ü∂ I.subscheme :=
  J.subschemeCover.openCover.glueMorphisms (fun U ‚Ü¶ glueDataObjHom h U ‚â´ I.subschemeCover.map U)
  (by
    intro U V
    simp only [‚Üê cancel_mono I.subschemeŒπ, AffineOpenCover.openCover_obj, glueDataObjHom_Œπ_assoc,
      AffineOpenCover.openCover_map, Category.assoc, subschemeCover_map_subschemeŒπ]
    rw [‚Üê subschemeCover_map_subschemeŒπ, pullback.condition_assoc, subschemeCover_map_subschemeŒπ])

@[reassoc (attr := simp)]
lemma subSchemeCover_map_inclusion {I J : IdealSheafData X} (h : I ‚â§ J) (U) :
    J.subschemeCover.map U ‚â´ inclusion h = glueDataObjHom h U ‚â´ I.subschemeCover.map U :=
  J.subschemeCover.openCover.Œπ_glueMorphisms _ _ _

@[reassoc (attr := simp)]
lemma inclusion_subschemeŒπ {I J : IdealSheafData X} (h : I ‚â§ J) :
    inclusion h ‚â´ I.subschemeŒπ = J.subschemeŒπ :=
  J.subschemeCover.openCover.hom_ext _ _ fun _ ‚Ü¶ by simp

@[simp, reassoc]
lemma inclusion_id (I : IdealSheafData X) :
    inclusion le_rfl = ùüô I.subscheme :=
  I.subschemeCover.openCover.hom_ext _ _ fun _ ‚Ü¶ by simp

@[reassoc (attr := simp)]
lemma inclusion_comp {I J K : IdealSheafData X} (h‚ÇÅ : I ‚â§ J) (h‚ÇÇ : J ‚â§ K) :
    inclusion h‚ÇÇ ‚â´ inclusion h‚ÇÅ = inclusion (h‚ÇÅ.trans h‚ÇÇ) :=
  K.subschemeCover.openCover.hom_ext _ _ fun _ ‚Ü¶ by simp

/-- The functor taking an ideal sheaf to its associated subscheme. -/
@[simps]
noncomputable
def subschemeFunctor (Y : Scheme.{u}) : (IdealSheafData Y)·µí·µñ ‚•§ Over Y where
  obj I := .mk I.unop.subschemeŒπ
  map {I J} h := Over.homMk (IdealSheafData.inclusion h.unop.le)

end IdealSheafData

end subscheme

noncomputable section image

open Limits

variable {X Y : Scheme.{u}} (f : X.Hom Y) (U : Y.affineOpens)

/-- The scheme theoretic image of a morphism. -/
abbrev Hom.image : Scheme.{u} := f.ker.subscheme

/-- The embedding from the scheme theoretic image to the codomain. -/
abbrev Hom.imageŒπ : f.image ‚ü∂ Y := f.ker.subschemeŒπ

lemma ideal_ker_le_ker_ŒìSpecIso_inv_comp :
    f.ker.ideal U ‚â§ RingHom.ker ((ŒìSpecIso Œì(Y, ‚ÜëU)).inv ‚â´
      (pullback.snd f U.1.Œπ ‚â´ U.1.toSpecŒì).appTop).hom := by
  let e : Œì(X, f ‚Åª¬π·µÅ ‚ÜëU) ‚âÖ Œì(Limits.pullback (C := Scheme) f U.1.Œπ, ‚ä§) :=
    X.presheaf.mapIso (eqToIso (by simp [IsOpenImmersion.opensRange_pullback_fst_of_right])).op
      ‚â™‚â´ (Limits.pullback.fst (C := Scheme) f U.1.Œπ).appIso ‚ä§
  have he : f.app U ‚â´ e.hom =
      (ŒìSpecIso Œì(Y, ‚ÜëU)).inv ‚â´ (pullback.snd f U.1.Œπ ‚â´ U.1.toSpecŒì).appTop := by
    rw [‚Üê (Iso.inv_comp_eq _).mpr U.2.isoSpec_inv_appTop, Category.assoc, Iso.eq_inv_comp]
    simp only [Opens.topIso_hom, eqToHom_op, Hom.app_eq_appLE, Iso.trans_hom, Functor.mapIso_hom,
      Iso.op_hom, eqToIso.hom, Hom.appIso_hom, Hom.appLE_map, Hom.map_appLE, appLE_comp_appLE,
      Opens.map_top, e, pullback.condition, IsAffineOpen.toSpecŒì_isoSpec_inv, Category.assoc]
    rw [comp_appLE, Opens.Œπ_app]
    exact Hom.map_appLE _ _ (homOfLE le_top).op
  rw [‚Üê he]
  refine (IdealSheafData.ideal_ofIdeals_le _ _).trans_eq
    (RingHom.ker_equiv_comp _ e.commRingCatIsoToRingEquiv).symm

private noncomputable
def Hom.toImageAux : X ‚ü∂ f.image :=
  ((Y.openCoverOfISupEqTop _ (iSup_affineOpens_eq_top Y)).pullbackCover f).glueMorphisms
    (fun U ‚Ü¶ (pullback.snd f U.1.Œπ ‚â´ U.1.toSpecŒì).liftQuotient _
      (by exact ideal_ker_le_ker_ŒìSpecIso_inv_comp f U) ‚â´ f.ker.subschemeCover.map U) (by
    intros U V
    rw [‚Üê cancel_mono f.imageŒπ]
    simp [IdealSheafData.glueDataObjŒπ, Scheme.Hom.liftQuotient_comp_assoc,
      ‚Üê pullback.condition, ‚Üê pullback.condition_assoc])

private lemma Hom.toImageAux_spec :
    f.toImageAux ‚â´ f.imageŒπ = f := by
  apply ((Y.openCoverOfISupEqTop _ (iSup_affineOpens_eq_top Y)).pullbackCover f).hom_ext
  intro U
  simp only [Hom.toImageAux, Cover.Œπ_glueMorphisms_assoc]
  simp [IdealSheafData.glueDataObjŒπ, Scheme.Hom.liftQuotient_comp_assoc, pullback.condition]

/-- The morphism from the domain to the scheme theoretic image. -/
noncomputable
def Hom.toImage : X ‚ü∂ f.image :=
  f.toImageAux.copyBase (fun x ‚Ü¶ ‚ü®f.base x, f.range_subset_ker_support ‚ü®x, rfl‚ü©‚ü©)
    (funext fun x ‚Ü¶ Subtype.ext congr(($f.toImageAux_spec).base x))

@[reassoc (attr := simp)]
lemma Hom.toImage_imageŒπ :
    f.toImage ‚â´ f.imageŒπ = f := by
  convert f.toImageAux_spec using 2
  exact Scheme.Hom.copyBase_eq _ _ _

instance [QuasiCompact f] : IsDominant f.toImage where
  denseRange := by
    rw [denseRange_iff_closure_range, f.imageŒπ.isEmbedding.closure_eq_preimage_closure_image,
      ‚Üê Set.univ_subset_iff, ‚Üê Set.image_subset_iff, Set.image_univ,
      IdealSheafData.range_subschemeŒπ, Hom.support_ker, ‚Üê Set.range_comp,
      ‚Üê TopCat.coe_comp, ‚Üê Scheme.comp_base, f.toImage_imageŒπ]

lemma Hom.toImage_app :
    f.toImage.app (f.imageŒπ ‚Åª¬π·µÅ U) =
      (f.ker.subschemeObjIso U).hom ‚â´ CommRingCat.ofHom
        (Ideal.Quotient.lift _ (f.app U.1).hom (IdealSheafData.ideal_ofIdeals_le _ _)) := by
  have := ConcreteCategory.epi_of_surjective _ (f.ker.subschemeŒπ_app_surjective U)
  rw [‚Üê cancel_epi (f.ker.subschemeŒπ.app U), ‚Üê Scheme.comp_app,
    Scheme.congr_app f.toImage_imageŒπ, f.ker.subschemeŒπ_app,
    ‚Üê IsIso.eq_comp_inv, ‚Üê Functor.map_inv]
  simp only [comp_coeBase, Opens.map_comp_obj, Category.assoc,
    Iso.inv_hom_id_assoc, eqToHom_op, inv_eqToHom]
  rw [‚Üê reassoc_of% CommRingCat.ofHom_comp, Ideal.Quotient.lift_comp_mk, CommRingCat.ofHom_hom,
    eqToHom_refl, CategoryTheory.Functor.map_id]
  exact (Category.comp_id _).symm

lemma Hom.toImage_app_injective [QuasiCompact f] :
    Function.Injective (f.toImage.app (f.imageŒπ ‚Åª¬π·µÅ U)) := by
  simp only [f.toImage_app U, CommRingCat.hom_comp, CommRingCat.hom_ofHom, RingHom.coe_comp]
  exact (RingHom.lift_injective_of_ker_le_ideal _ _ (by simp)).comp
    (f.ker.subschemeObjIso U).commRingCatIsoToRingEquiv.injective

lemma Hom.stalkFunctor_toImage_injective [QuasiCompact f] (x) :
    Function.Injective ((TopCat.Presheaf.stalkFunctor _ x).map f.toImage.c) := by
  apply TopCat.Presheaf.stalkFunctor_map_injective_of_isBasis
    (hB := ((isBasis_affine_open Y).of_isInducing f.imageŒπ.isEmbedding.isInducing))
  rintro _ ‚ü®U, hU, rfl‚ü©
  exact f.toImage_app_injective ‚ü®U, hU‚ü©

open IdealSheafData in
/-- The adjunction between `Y.IdealSheafData` and `(Over Y)·µí·µñ` given by taking kernels. -/
@[simps]
noncomputable
def kerAdjunction (Y : Scheme.{u}) : (subschemeFunctor Y).rightOp ‚ä£ Y.kerFunctor where
  unit.app I := eqToHom (by simp)
  counit.app f := (Over.homMk f.unop.hom.toImage f.unop.hom.toImage_imageŒπ).op
  counit.naturality _ _ _ := Quiver.Hom.unop_inj (by ext1; simp [‚Üê cancel_mono (subschemeŒπ _)])
  left_triangle_components I := Quiver.Hom.unop_inj (by ext1; simp [‚Üê cancel_mono (subschemeŒπ _)])

instance : (IdealSheafData.subschemeFunctor Y).Full :=
  have : IsIso Y.kerAdjunction.rightOp.counit := by
    simp [NatTrans.isIso_iff_isIso_app, CategoryTheory.instIsIsoEqToHom]
  Y.kerAdjunction.rightOp.fullyFaithfulROfIsIsoCounit.full

end image

end Scheme

end AlgebraicGeometry
