/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten, Andrew Yang
-/
import Mathlib.AlgebraicGeometry.AffineScheme
import Mathlib.AlgebraicGeometry.Cover.Open
import Mathlib.AlgebraicGeometry.GammaSpecAdjunction
import Mathlib.AlgebraicGeometry.Restrict

/-!

# Nilpotents and zero locus on schemes

In this file we show some properties of the zero locus of a set of sections specific
to locally ringed spaces or schemes and characterise nilpotent sections in terms of
the associated basic open.

In the case of affine schemes, the zero locus corresponds under the `Scheme.isoSpec` isomorphism
to the zero locus in the sense of prime spectra. We provide API for reasoning about zero loci on
affine schemes to avoid using the `Scheme.isoSpec` isomorphism in applications.

## Main results

- `Scheme.isNilpotent_of_basicOpen_eq_bot_of_isCompact`: a section over a compact open
  of a scheme is nilpotent if and only if the associated basic open is empty.

## TODO

- Relate the zero locus to evaluation of sections in the residue fields of points.

-/

universe u

open TopologicalSpace CategoryTheory Opposite

noncomputable section

namespace AlgebraicGeometry

section toÎ“SpecFun

/-- On an affine scheme, `toÎ“SpecFun` is bijective. -/
lemma Scheme.toÎ“SpecFun_bijective_of_isAffine (X : Scheme.{u}) [IsAffine X] :
    Function.Bijective X.toÎ“SpecFun :=
  (ConcreteCategory.bijective_of_isIso X.isoSpec.hom.val.base)

/-- On an affine scheme, `toÎ“SpecFun` is a closed map. -/
lemma Scheme.toÎ“SpecFun_isClosedMap (X : Scheme.{u}) [IsAffine X] :
    IsClosedMap X.toÎ“SpecFun :=
  (TopCat.homeoOfIso (asIso <| X.isoSpec.hom.val.base)).isClosedMap

end toÎ“SpecFun

section Cover

/-- If two global sections agree after restriction to each member of a finite open cover, then
they agree globally. -/
lemma Scheme.eq_of_eq_cover {X : Scheme.{u}} (f g : Î“(X, âŠ¤)) (ð’° : X.OpenCover)
    (h : âˆ€ i : ð’°.J, Scheme.Î“.map (ð’°.map i).op f = Scheme.Î“.map (ð’°.map i).op g) : f = g := by
  fapply TopCat.Sheaf.eq_of_locally_eq' X.sheaf
    (fun i â†¦ Scheme.Hom.opensRange (ð’°.map (ð’°.f i))) _ (fun _ â†¦ homOfLE le_top)
  Â· rintro x -; simpa using âŸ¨_, ð’°.covers xâŸ©
  Â· intro x;
    replace h := h (ð’°.f x)
    rw [â† IsOpenImmersion.map_Î“Iso_inv] at h
    exact (IsOpenImmersion.Î“Iso (ð’°.map (ð’°.f x))).commRingCatIsoToRingEquiv.symm.injective h

/-- If the restriction of a global section to each member of an open cover is zero, then it is
globally zero. -/
lemma Scheme.zero_of_zero_cover {X : Scheme.{u}} (s : Î“(X, âŠ¤)) (ð’° : X.OpenCover)
    (h : âˆ€ i : ð’°.J, Scheme.Î“.map (ð’°.map i).op s = 0) : s = 0 := by
  refine eq_of_eq_cover s 0 ð’° (fun i â†¦ by rw [map_zero]; exact h i)

end Cover

section ZeroLocus

lemma Scheme.zeroLocus_def {X : Scheme.{u}} {U : Opens X} (s : Set Î“(X, U)) :
    X.toRingedSpace.zeroLocus s = â‹‚ f âˆˆ s, (X.basicOpen f).carrierá¶œ :=
  rfl

/-- On a locally ringed space `X`, the preimage of the zero locus of the prime spectrum
of `Î“(X, âŠ¤)` under `toÎ“SpecFun` agrees with the associated zero locus on `X`. -/
lemma LocallyRingedSpace.toÎ“Spec_preim_zeroLocus_eq {X : LocallyRingedSpace}
    (s : Set (X.presheaf.obj (op âŠ¤))) :
    X.toÎ“SpecFun â»Â¹' PrimeSpectrum.zeroLocus s = X.toRingedSpace.zeroLocus s := by
  simp only [RingedSpace.zeroLocus]
  have (i : LocallyRingedSpace.Î“.obj (op X)) (_ : i âˆˆ s) :
      ((X.toRingedSpace.basicOpen i).carrier)á¶œ =
        X.toÎ“SpecFun â»Â¹' (PrimeSpectrum.basicOpen i).carrierá¶œ := by
    symm
    rw [Set.preimage_compl, X.toÎ“Spec_preim_basicOpen_eq i]
  erw [Set.iInterâ‚‚_congr this]
  simp only [â† Set.preimage_iInterâ‚‚, Î“_obj, Opens.carrier_eq_coe,
    PrimeSpectrum.basicOpen_eq_zeroLocus_compl, compl_compl, â† PrimeSpectrum.zeroLocus_iUnionâ‚‚]
  simp

/-- If `X` is affine, the image of the zero locus of global sections of `X` under `toÎ“SpecFun`
is the zero locus in terms of the prime spectrum of `Î“(X, âŠ¤)`. -/
lemma Scheme.toÎ“Spec_image_zeroLocus_eq {X : Scheme} [IsAffine X] (s : Set Î“(X, âŠ¤)) :
    X.toÎ“SpecFun '' X.toRingedSpace.zeroLocus s = PrimeSpectrum.zeroLocus s := by
  rw [â† X.toÎ“Spec_preim_zeroLocus_eq, Set.image_preimage_eq]
  exact X.toÎ“SpecFun_bijective_of_isAffine.surjective

/-- If `X` is an affine scheme, every closed set of `X` is the zero locus
of a set of global sections. -/
lemma eq_zeroLocus_of_isClosed (X : Scheme) [IsAffine X] (s : Set X) (hs : IsClosed s) :
    âˆƒ I : Ideal (Î“(X, âŠ¤)), s = X.toRingedSpace.zeroLocus (I : Set Î“(X, âŠ¤)) := by
  let Z : Set (Spec <| Î“(X, âŠ¤)) := X.toÎ“SpecFun '' s
  have hZ : IsClosed Z := X.toÎ“SpecFun_isClosedMap _ hs
  obtain âŸ¨I, (hI : Z = _)âŸ© := (PrimeSpectrum.isClosed_iff_zeroLocus_ideal _).mp hZ
  use I
  simp only [â† LocallyRingedSpace.toÎ“Spec_preim_zeroLocus_eq, â† hI, Z]
  rw [Set.preimage_image_eq _ X.toÎ“SpecFun_bijective_of_isAffine.injective]

end ZeroLocus

section Nilpotents

/-- If a global section is nilpotent on each member of a finite open cover, then `f` is
nilpotent. -/
lemma Scheme.isNilpotent_of_isNilpotent_cover {X : Scheme.{u}} (s : Î“(X, âŠ¤)) (ð’° : X.OpenCover)
    [Finite ð’°.J] (h : âˆ€ i : ð’°.J, IsNilpotent (Scheme.Î“.map (ð’°.map i).op s)) : IsNilpotent s := by
  choose fn hfn using h
  have : Fintype ð’°.J := Fintype.ofFinite ð’°.J
  /- the maximum of all `fn i` (exists, because `ð’°.J` is finite) -/
  let N : â„• := Finset.sup Finset.univ fn
  have hfnleN (i : ð’°.J) : fn i â‰¤ N := Finset.le_sup (Finset.mem_univ i)
  use N
  apply zero_of_zero_cover
  intro i
  simp only [map_pow]
  exact pow_eq_zero_of_le (hfnleN i) (hfn i)

/-- If `U` is an affine open of a scheme `X`, a section over `U` is nilpotent if and only if
the associated basic open is empty.
See `Scheme.isNilpotent_iff_basicOpen_eq_bot_of_isCompact` for a version only assuming
`U` to be compact. -/
lemma IsAffineOpen.isNilpotent_iff_basicOpen_eq_bot {X : Scheme} {U : Opens X} (hU : IsAffineOpen U)
    (f : Î“(X, U)) : IsNilpotent f â†” X.basicOpen f = âŠ¥ := by
  constructor
  Â· intro h
    apply X.basicOpen_eq_bot_of_isNilpotent
    exact h
  Â· intro h
    rw [â† PrimeSpectrum.basicOpen_eq_bot_iff, â† hU.fromSpec_preimage_basicOpen, h]
    rfl

/-- If `X` is an affine scheme and `f` is a global section, then `f` is nilpotent if and only
if the associated basic open is empty. -/
lemma Scheme.isNilpotent_iff_basicOpen_eq_bot_of_isAffine (X : Scheme) [IsAffine X] (f : Î“(X, âŠ¤)) :
    IsNilpotent f â†” X.basicOpen f = âŠ¥ :=
  (isAffineOpen_top X).isNilpotent_iff_basicOpen_eq_bot f


instance Scheme.Î¹Opens_appLE_isIso {X : Scheme.{u}} (U : Opens X) :
    IsIso ((Scheme.Î¹Opens U).appLE U âŠ¤ (by simp)) := by
  simp only [restrict_presheaf_obj, ofRestrict_appLE]
  let e : (Scheme.Î¹Opens U) ''áµ âŠ¤ â‰¤ U := by simp
  have : IsIso (homOfLE e) := by
    apply homOfLE_isIso_of_eq
    simp
  apply Functor.map_isIso

/-- If `U` is a compact open of a scheme and `f` a section over `U` such that the associated
basic open is empty, then `f` is nilpotent. This is the harder direction of
`Scheme.isNilpotent_iff_basicOpen_eq_bot_of_isCompact`. -/
lemma Scheme.isNilpotent_of_basicOpen_eq_bot_of_isCompact {X : Scheme.{u}}
    {U : Opens X} (hU : IsCompact U.carrier) (f : Î“(X, U))
    (hf : X.basicOpen f = âŠ¥) : IsNilpotent f := by
  let U' : Scheme.{u} := X âˆ£_áµ¤ U
  have : CompactSpace U' := (isCompact_iff_compactSpace).mp hU
  let f' : Î“(U', âŠ¤) := (X.Î¹Opens U).appLE U âŠ¤ (by simp) f
  have hf' : U'.basicOpen f' = âŠ¥ := by
    simp only [f', Scheme.basicOpen_appLE, hf, ofRestrict_val_base, ge_iff_le, le_top,
      inf_of_le_right]
    rfl
  let ð’° : U'.OpenCover := U'.affineCover.finiteSubcover
  have (i : ð’°.J) : IsAffine (ð’°.obj i) := Scheme.isAffine_affineCover U' _
  have hf' : IsNilpotent f' := by
    refine isNilpotent_of_isNilpotent_cover (X := U') f' ð’° (fun i â†¦ ?_)
    rw [Scheme.isNilpotent_iff_basicOpen_eq_bot_of_isAffine (ð’°.obj i)]
    simp only [Î“_obj, Î“_map, Quiver.Hom.unop_op]
    erw [â† preimage_basicOpen]
    rw [hf']
    rfl
  have hfinj : Function.Injective ((Hom.appLE (Î¹Opens U) U âŠ¤ (by simp))) := by
    apply Function.Bijective.injective
    exact ConcreteCategory.bijective_of_isIso (Hom.appLE (Î¹Opens U) U âŠ¤ _)
  rw [â† IsNilpotent.map_iff hfinj]
  exact hf'

/-- A section over a compact open of a scheme is nilpotent if and only if its associated
basic open is empty. -/
lemma Scheme.isNilpotent_iff_basicOpen_eq_bot_of_isCompact {X : Scheme}
    {U : Opens X} (hU : IsCompact U.carrier) (f : Î“(X, U)) :
    IsNilpotent f â†” X.basicOpen f = âŠ¥ :=
  âŸ¨X.basicOpen_eq_bot_of_isNilpotent U f,
    Scheme.isNilpotent_of_basicOpen_eq_bot_of_isCompact hU fâŸ©

/-- The zero locus of a set of sections over a compact open of a scheme is `X` if and only if
`s` is contained in the nilradical of `Î“(X, U)`. -/
lemma Scheme.zeroLocus_eq_top_iff_subset_nilradical_of_isCompact {X : Scheme.{u}} {U : Opens X}
    (hU : IsCompact U.carrier) (s : Set Î“(X, U)) :
    X.toRingedSpace.zeroLocus s = âŠ¤ â†” s âŠ† (nilradical Î“(X, U)).carrier := by
  simp [Scheme.zeroLocus_def, â† Scheme.isNilpotent_iff_basicOpen_eq_bot_of_isCompact hU,
    â† mem_nilradical, Set.subset_def]

end Nilpotents

end AlgebraicGeometry
