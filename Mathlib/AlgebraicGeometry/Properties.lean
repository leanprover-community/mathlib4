/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.AffineScheme
import Mathlib.RingTheory.LocalProperties.Reduced
import Mathlib.Topology.KrullDimension
import Mathlib.RingTheory.Ideal.Height

/-!
# Basic properties of schemes

We provide some basic properties of schemes

## Main definition
* `AlgebraicGeometry.IsIntegral`: A scheme is integral if it is nontrivial and all nontrivial
  components of the structure sheaf are integral domains.
* `AlgebraicGeometry.IsReduced`: A scheme is reduced if all the components of the structure sheaf
  are reduced.
-/


-- Explicit universe annotations were used in this file to improve performance https://github.com/leanprover-community/mathlib4/issues/12737

universe u

open TopologicalSpace Opposite CategoryTheory CategoryTheory.Limits TopCat Topology

namespace AlgebraicGeometry

variable (X : Scheme)

instance : T0Space X :=
  T0Space.of_open_cover fun x => âŸ¨_, X.affineCover.covers x,
    (X.affineCover.f x).opensRange.2, IsEmbedding.t0Space (Y := PrimeSpectrum _)
    (isAffineOpen_opensRange (X.affineCover.f x)).isoSpec.schemeIsoToHomeo.isEmbeddingâŸ©

instance : QuasiSober X := by
  apply (config := { allowSynthFailures := true })
    quasiSober_of_open_cover (Set.range fun x => Set.range <| (X.affineCover.f x).base)
  Â· rintro âŸ¨_, i, rflâŸ©; exact (X.affineCover.map_prop i).base_open.isOpen_range
  Â· rintro âŸ¨_, i, rflâŸ©
    exact @IsOpenEmbedding.quasiSober _ _ _ _ _
      (X.affineCover.map_prop i).base_open.isEmbedding.toHomeomorph.symm.isOpenEmbedding
        PrimeSpectrum.quasiSober
  Â· rw [Set.top_eq_univ, Set.sUnion_range, Set.eq_univ_iff_forall]
    intro x; exact âŸ¨_, âŸ¨_, rflâŸ©, X.affineCover.covers xâŸ©

instance {X : Scheme.{u}} : PrespectralSpace X :=
  have (Y : Scheme.{u}) (_ : IsAffine Y) : PrespectralSpace Y :=
    .of_isClosedEmbedding (Y := PrimeSpectrum _) _
      Y.isoSpec.hom.homeomorph.isClosedEmbedding
  have (i : _) : PrespectralSpace (X.affineCover.f i).opensRange.1 :=
    this (X.affineCover.f i).opensRange (isAffineOpen_opensRange (X.affineCover.f i))
  .of_isOpenCover X.affineCover.isOpenCover_opensRange

/-- A scheme `X` is reduced if all `ğ’ªâ‚“(U)` are reduced. -/
class IsReduced : Prop where
  component_reduced : âˆ€ U, _root_.IsReduced Î“(X, U) := by infer_instance

attribute [instance] IsReduced.component_reduced

theorem isReduced_of_isReduced_stalk [âˆ€ x : X, _root_.IsReduced (X.presheaf.stalk x)] :
    IsReduced X := by
  refine âŸ¨fun U => âŸ¨fun s hs => ?_âŸ©âŸ©
  apply Presheaf.section_ext X.sheaf U s 0
  intro x hx
  change (X.sheaf.presheaf.germ U x hx) s = (X.sheaf.presheaf.germ U x hx) 0
  rw [RingHom.map_zero]
  change X.presheaf.germ U x hx s = 0
  exact (hs.map _).eq_zero

instance isReduced_stalk_of_isReduced [IsReduced X] (x : X) :
    _root_.IsReduced (X.presheaf.stalk x) := by
  constructor
  rintro g âŸ¨n, eâŸ©
  obtain âŸ¨U, hxU, s, (rfl : (X.presheaf.germ U x hxU) s = g)âŸ© := X.presheaf.germ_exist x g
  rw [â† map_pow, â† map_zero (X.presheaf.germ _ x hxU).hom] at e
  obtain âŸ¨V, hxV, iU, iV, (e' : (X.presheaf.map iU.op) (s ^ n) = (X.presheaf.map iV.op) 0)âŸ© :=
    X.presheaf.germ_eq x hxU hxU _ 0 e
  rw [map_pow, map_zero] at e'
  replace e' := (IsNilpotent.mk _ _ e').eq_zero (R := Î“(X, V))
  rw [â† X.presheaf.germ_res iU x hxV, CommRingCat.comp_apply, e', map_zero]

theorem isReduced_of_isOpenImmersion {X Y : Scheme} (f : X âŸ¶ Y) [H : IsOpenImmersion f]
    [IsReduced Y] : IsReduced X := by
  constructor
  intro U
  have : U = f â»Â¹áµ f ''áµ U := by
    ext1; exact (Set.preimage_image_eq _ H.base_open.injective).symm
  rw [this]
  exact isReduced_of_injective (inv <| f.app (f ''áµ U)).hom
    (asIso <| f.app (f ''áµ U) : Î“(Y, f ''áµ U) â‰… _).symm.commRingCatIsoToRingEquiv.injective

instance {R : CommRingCat.{u}} [H : _root_.IsReduced R] : IsReduced (Spec R) := by
  apply (config := { allowSynthFailures := true }) isReduced_of_isReduced_stalk
  intro x; dsimp
  have : _root_.IsReduced (CommRingCat.of <| Localization.AtPrime (PrimeSpectrum.asIdeal x)) := by
    dsimp; infer_instance
  exact isReduced_of_injective (StructureSheaf.stalkIso R x).hom.hom
    (StructureSheaf.stalkIso R x).commRingCatIsoToRingEquiv.injective

theorem affine_isReduced_iff (R : CommRingCat) :
    IsReduced (Spec R) â†” _root_.IsReduced R := by
  refine âŸ¨?_, fun h => inferInstanceâŸ©
  intro h
  exact isReduced_of_injective (Scheme.Î“SpecIso R).inv.hom
    (Scheme.Î“SpecIso R).symm.commRingCatIsoToRingEquiv.injective

theorem isReduced_of_isAffine_isReduced [IsAffine X] [_root_.IsReduced Î“(X, âŠ¤)] :
    IsReduced X :=
  isReduced_of_isOpenImmersion X.isoSpec.hom

/-- To show that a statement `P` holds for all open subsets of all schemes, it suffices to show that
1. In any scheme `X`, if `P` holds for an open cover of `U`, then `P` holds for `U`.
2. For an open immersion `f : X âŸ¶ Y`, if `P` holds for the entire space of `X`, then `P` holds for
  the image of `f`.
3. `P` holds for the entire space of an affine scheme.
-/
@[elab_as_elim]
theorem reduce_to_affine_global (P : âˆ€ {X : Scheme} (_ : X.Opens), Prop)
    {X : Scheme} (U : X.Opens)
    (hâ‚ : âˆ€ (X : Scheme) (U : X.Opens),
      (âˆ€ x : U, âˆƒ (V : _) (_ : x.1 âˆˆ V) (_ : V âŸ¶ U), P V) â†’ P U)
    (hâ‚‚ : âˆ€ (X Y) (f : X âŸ¶ Y) [IsOpenImmersion f],
      âˆƒ (U : X.Opens) (V : Y.Opens), U = âŠ¤ âˆ§ V = f.opensRange âˆ§ (P U â†’ P V))
    (hâ‚ƒ : âˆ€ R : CommRingCat, P (X := Spec R) âŠ¤) : P U := by
  apply hâ‚
  intro x
  obtain âŸ¨_, âŸ¨j, rflâŸ©, hx, iâŸ© :=
    X.affineBasisCover_is_basis.exists_subset_of_mem_open (SetLike.mem_coe.2 x.prop) U.isOpen
  let U' : Opens _ := âŸ¨_, (X.affineBasisCover.map_prop j).base_open.isOpen_rangeâŸ©
  let i' : U' âŸ¶ U := homOfLE i
  refine âŸ¨U', hx, i', ?_âŸ©
  obtain âŸ¨_, _, rfl, rfl, hâ‚‚'âŸ© := hâ‚‚ _ _ (X.affineBasisCover.f j)
  apply hâ‚‚'
  apply hâ‚ƒ

theorem reduce_to_affine_nbhd (P : âˆ€ (X : Scheme) (_ : X), Prop)
    (hâ‚ : âˆ€ R x, P (Spec R) x)
    (hâ‚‚ : âˆ€ {X Y} (f : X âŸ¶ Y) [IsOpenImmersion f] (x : X), P X x â†’ P Y (f.base x)) :
    âˆ€ (X : Scheme) (x : X), P X x := by
  intro X x
  obtain âŸ¨y, eâŸ© := X.affineCover.covers x
  convert hâ‚‚ (X.affineCover.f (X.affineCover.idx x)) y _
  Â· rw [e]
  apply hâ‚

theorem eq_zero_of_basicOpen_eq_bot {X : Scheme} [hX : IsReduced X] {U : X.Opens}
    (s : Î“(X, U)) (hs : X.basicOpen s = âŠ¥) : s = 0 := by
  apply TopCat.Presheaf.section_ext X.sheaf U
  intro x hx
  change (X.sheaf.presheaf.germ U x hx) s = (X.sheaf.presheaf.germ U x hx) 0
  rw [RingHom.map_zero]
  induction U using reduce_to_affine_global generalizing hX with
  | hâ‚ X U H =>
    obtain âŸ¨V, hx, i, HâŸ© := H âŸ¨x, hxâŸ©
    specialize H (X.presheaf.map i.op s)
    rw [Scheme.basicOpen_res, hs] at H
    specialize H (inf_bot_eq _) x hx
    rw [â† X.sheaf.presheaf.germ_res_apply i x hx s]
    exact H
  | hâ‚‚ X Y f =>
    refine âŸ¨f â»Â¹áµ f.opensRange, f.opensRange, by simp, rfl, ?_âŸ©
    rintro H hX s hs _ âŸ¨x, rflâŸ©
    haveI := isReduced_of_isOpenImmersion f
    specialize H (f.app _ s) _ x âŸ¨x, rflâŸ©
    Â· rw [â† Scheme.preimage_basicOpen, hs]; ext1; simp [Opens.map]
    Â· have H : (X.presheaf.germ _ x _).hom _ = 0 := H
      rw [â† Scheme.stalkMap_germ_apply f âŸ¨_, _âŸ© x] at H
      apply_fun inv <| f.stalkMap x at H
      rw [â† CommRingCat.comp_apply, CategoryTheory.IsIso.hom_inv_id, map_zero] at H
      exact H
  | hâ‚ƒ R =>
    rw [basicOpen_eq_of_affine', PrimeSpectrum.basicOpen_eq_bot_iff] at hs
    replace hs := (hs.map (Scheme.Î“SpecIso R).inv.hom).eq_zero
    rw [â† CommRingCat.comp_apply, Iso.hom_inv_id, CommRingCat.id_apply] at hs
    rw [hs, map_zero]

@[simp]
theorem basicOpen_eq_bot_iff {X : Scheme} [IsReduced X] {U : X.Opens}
    (s : Î“(X, U)) : X.basicOpen s = âŠ¥ â†” s = 0 := by
  refine âŸ¨eq_zero_of_basicOpen_eq_bot s, ?_âŸ©
  rintro rfl
  simp

/-- A scheme `X` is integral if its is nonempty,
and `ğ’ªâ‚“(U)` is an integral domain for each `U â‰  âˆ…`. -/
class IsIntegral : Prop where
  nonempty : Nonempty X := by infer_instance
  component_integral : âˆ€ (U : X.Opens) [Nonempty U], IsDomain Î“(X, U) := by infer_instance

attribute [instance] IsIntegral.component_integral IsIntegral.nonempty

instance [IsIntegral X] : IsDomain Î“(X, âŠ¤) :=
  @IsIntegral.component_integral _ _ _ âŸ¨Nonempty.some inferInstance, trivialâŸ©

instance (priority := 900) isReduced_of_isIntegral [IsIntegral X] : IsReduced X := by
  constructor
  intro U
  rcases U.1.eq_empty_or_nonempty with h | h
  Â· have : U = âŠ¥ := SetLike.ext' h
    haveI : Subsingleton Î“(X, U) :=
      CommRingCat.subsingleton_of_isTerminal (X.sheaf.isTerminalOfEqEmpty this)
    infer_instance
  Â· haveI : Nonempty U := by simpa
    infer_instance

instance Scheme.component_nontrivial (X : Scheme.{u}) (U : X.Opens) [Nonempty U] :
    Nontrivial Î“(X, U) :=
  LocallyRingedSpace.component_nontrivial (hU := â€¹_â€º)

instance irreducibleSpace_of_isIntegral [IsIntegral X] : IrreducibleSpace X := by
  by_contra H
  replace H : Â¬IsPreirreducible (âŠ¤ : Set X) := fun h =>
    H { toPreirreducibleSpace := âŸ¨hâŸ©
        toNonempty := inferInstance }
  simp_rw [isPreirreducible_iff_isClosed_union_isClosed, not_forall, not_or] at H
  rcases H with âŸ¨S, T, hS, hT, hâ‚, hâ‚‚, hâ‚ƒâŸ©
  rw [Set.not_top_subset] at hâ‚‚ hâ‚ƒ
  haveI : Nonempty (âŸ¨Sá¶œ, hS.1âŸ© : X.Opens) := âŸ¨âŸ¨_, hâ‚‚.choose_specâŸ©âŸ©
  haveI : Nonempty (âŸ¨Tá¶œ, hT.1âŸ© : X.Opens) := âŸ¨âŸ¨_, hâ‚ƒ.choose_specâŸ©âŸ©
  haveI : Nonempty (âŸ¨Sá¶œ, hS.1âŸ© âŠ” âŸ¨Tá¶œ, hT.1âŸ© : X.Opens) := âŸ¨âŸ¨_, Or.inl hâ‚‚.choose_specâŸ©âŸ©
  let e : Î“(X, _) â‰… CommRingCat.of _ :=
    (X.sheaf.isProductOfDisjoint âŸ¨_, hS.1âŸ© âŸ¨_, hT.1âŸ© ?_).conePointUniqueUpToIso
      (CommRingCat.prodFanIsLimit _ _)
  Â· have : IsDomain (Î“(X, âŸ¨Sá¶œ, hS.1âŸ©) Ã— Î“(X, âŸ¨Tá¶œ, hT.1âŸ©)) :=
      e.symm.commRingCatIsoToRingEquiv.toMulEquiv.isDomain _
    exact false_of_nontrivial_of_product_domain Î“(X, âŸ¨Sá¶œ, hS.1âŸ©) Î“(X, âŸ¨Tá¶œ, hT.1âŸ©)
  Â· ext x
    constructor
    Â· rintro âŸ¨hS, hTâŸ©
      rcases hâ‚ (show x âˆˆ âŠ¤ by trivial) with h | h
      exacts [hS h, hT h]
    Â· simp

theorem isIntegral_of_irreducibleSpace_of_isReduced [IsReduced X] [H : IrreducibleSpace X] :
    IsIntegral X := by
  constructor; Â· infer_instance
  intro U hU
  haveI := (@LocallyRingedSpace.component_nontrivial X.toLocallyRingedSpace U hU).1
  have : NoZeroDivisors
      (X.toLocallyRingedSpace.toSheafedSpace.toPresheafedSpace.presheaf.obj (op U)) := by
    refine âŸ¨fun {a b} e => ?_âŸ©
    simp_rw [â† basicOpen_eq_bot_iff, â† Opens.not_nonempty_iff_eq_bot]
    by_contra! h
    obtain âŸ¨x, âŸ¨hxU, hxâ‚âŸ©, _, hxâ‚‚âŸ© :=
      nonempty_preirreducible_inter (X.basicOpen a).2 (X.basicOpen b).2 h.1 h.2
    replace e := congr_arg (X.presheaf.germ U x hxU) e
    rw [RingHom.map_mul, RingHom.map_zero] at e
    refine zero_ne_one' (X.presheaf.stalk x) (isUnit_zero_iff.1 ?_)
    convert hxâ‚.mul hxâ‚‚
    exact e.symm
  exact NoZeroDivisors.to_isDomain _

theorem isIntegral_iff_irreducibleSpace_and_isReduced :
    IsIntegral X â†” IrreducibleSpace X âˆ§ IsReduced X :=
  âŸ¨fun _ => âŸ¨inferInstance, inferInstanceâŸ©, fun âŸ¨_, _âŸ© =>
    isIntegral_of_irreducibleSpace_of_isReduced XâŸ©

theorem isIntegral_of_isOpenImmersion {X Y : Scheme} (f : X âŸ¶ Y) [H : IsOpenImmersion f]
    [IsIntegral Y] [Nonempty X] : IsIntegral X := by
  constructor; Â· infer_instance
  intro U hU
  have : U = f â»Â¹áµ f ''áµ U := by ext1; exact (Set.preimage_image_eq _ H.base_open.injective).symm
  rw [this]
  have : IsDomain Î“(Y, f ''áµ U) := by
    apply (config := { allowSynthFailures := true }) IsIntegral.component_integral
    exact âŸ¨âŸ¨_, _, hU.some.prop, rflâŸ©âŸ©
  exact (asIso <| f.app (f ''áµ U) :
    Î“(Y, f ''áµ U) â‰… _).symm.commRingCatIsoToRingEquiv.toMulEquiv.isDomain _

instance {R : CommRingCat} [IsDomain R] : IrreducibleSpace (Spec R) := by
  convert PrimeSpectrum.irreducibleSpace (R := R)

instance {R : CommRingCat} [IsDomain R] : IsIntegral (Spec R) :=
  isIntegral_of_irreducibleSpace_of_isReduced _

theorem affine_isIntegral_iff (R : CommRingCat) :
    IsIntegral (Spec R) â†” IsDomain R :=
  âŸ¨fun _ => MulEquiv.isDomain Î“(Spec R, âŠ¤)
    (Scheme.Î“SpecIso R).symm.commRingCatIsoToRingEquiv.toMulEquiv, fun _ => inferInstanceâŸ©

theorem isIntegral_of_isAffine_of_isDomain [IsAffine X] [Nonempty X] [IsDomain Î“(X, âŠ¤)] :
    IsIntegral X :=
  isIntegral_of_isOpenImmersion X.isoSpec.hom

theorem map_injective_of_isIntegral [IsIntegral X] {U V : X.Opens} (i : U âŸ¶ V)
    [H : Nonempty U] : Function.Injective (X.presheaf.map i.op) := by
  rw [injective_iff_map_eq_zero]
  intro x hx
  rw [â† basicOpen_eq_bot_iff] at hx âŠ¢
  rw [Scheme.basicOpen_res] at hx
  revert hx
  contrapose!
  simp_rw [Ne, â† Opens.not_nonempty_iff_eq_bot, Classical.not_not]
  apply nonempty_preirreducible_inter U.isOpen (RingedSpace.basicOpen _ _).isOpen
  simpa using H

noncomputable
instance [IsIntegral X] : OrderTop X where
  top := genericPoint X
  le_top a := genericPoint_specializes a

open IrreducibleCloseds Set in
@[stacks 02I4]
lemma coheight_eq_of_openImmersion {U X : Scheme} {Z : U} (f : U âŸ¶ X)
  [k : IsOpenImmersion f] : Order.coheight (f.base Z) = Order.coheight Z := by
  rw [â† Order.coheight_orderIso (irreducibleSetEquivPoints (Î± := X)).symm (f.base Z),
      â† Order.coheight_orderIso (irreducibleSetEquivPoints (Î± := U)).symm Z,
      â† Order.coheight_orderIso
      (map'OrderIso f.base (Scheme.Hom.continuous f) k.base_open)
      ((irreducibleSetEquivPoints (Î± := U)).symm Z)]
  let g : {V : IrreducibleCloseds X | â‡‘(ConcreteCategory.hom f.base) â»Â¹' â†‘V â‰  âˆ…} â†ªo
      IrreducibleCloseds X :=
    OrderEmbedding.subtype {V : IrreducibleCloseds X | â‡‘(ConcreteCategory.hom f.base) â»Â¹' V â‰  âˆ…}
  let a := (map'OrderIso f.base (Scheme.Hom.continuous f) k.base_open)
      (irreducibleSetEquivPoints.symm Z)
  have : âˆ€ p : LTSeries (IrreducibleCloseds X), p.head = g a â†’
         âˆƒ p' : LTSeries ({V : IrreducibleCloseds X | â‡‘(ConcreteCategory.hom f.base) â»Â¹' â†‘V â‰  âˆ…}),
           p'.head = a âˆ§ p = p'.map g (OrderEmbedding.strictMono g) := fun p hp â†¦ by
    let p' : LTSeries {V : IrreducibleCloseds X | â‡‘(ConcreteCategory.hom f.base) â»Â¹' â†‘V â‰  âˆ…} := {
      length := p.length
      toFun i := {
        val := p i
        property := by
          suffices  Â¬â‡‘(ConcreteCategory.hom f.base) â»Â¹' a = âˆ… by
            rw[â† Ne, â† nonempty_iff_ne_empty] at this
            exact nonempty_iff_ne_empty.mp <|
              Nonempty.mono (fun _ b â†¦ (hp â–¸ LTSeries.head_le p i) b) this
          exact a.2
      }
      step := p.step
    }
    exact âŸ¨p', SetCoe.ext hp, rflâŸ©
  have := Order.coheight_eq_of_strictMono g (fun _ _ a â†¦ a)
     ((map'OrderIso f.base (Scheme.Hom.continuous f) k.base_open)
     (irreducibleSetEquivPoints.symm Z)) this
  convert this.symm
  simp only [irreducibleSetEquivPoints, ne_eq, coe_setOf, mem_setOf_eq, map'OrderIso,
    RelIso.coe_fn_mk, Equiv.ofBijective_apply, map']
  suffices closure {f.base Z} = closure ((f.base) '' (closure {Z})) from
    IrreducibleCloseds.ext_iff.mpr this
  simp [closure_image_closure (Scheme.Hom.continuous f)]

open Order in
@[stacks 02IZ]
lemma stalk_dim_eq_coheight {X : Scheme} (Z : X) :
  ringKrullDim (X.presheaf.stalk Z) = Order.coheight Z := by
  obtain âŸ¨R, f, hfâŸ© := AlgebraicGeometry.Scheme.exists_affine_mem_range_and_range_subset
    (U := âŠ¤) (x := Z) (by aesop)
  obtain âŸ¨y, hyâŸ© := Set.mem_range.mp hf.2.1
  have := hf.1
  have := hy â–¸ AlgebraicGeometry.coheight_eq_of_openImmersion f (Z := y)
  rw [this]
  suffices ringKrullDim ((Spec R).presheaf.stalk y) = coheight y from
    this â–¸ Order.krullDim_eq_of_orderIso
    (hy â–¸ PrimeSpectrum.comapEquiv (asIso (Scheme.Hom.stalkMap f y)).commRingCatIsoToRingEquiv)
  let k : Algebra â†‘R â†‘((Spec R).presheaf.stalk y) := StructureSheaf.stalkAlgebra (â†‘R) y
  have : IsLocalization.AtPrime (â†‘((Spec R).presheaf.stalk y)) y.asIdeal :=
    StructureSheaf.IsLocalization.to_stalk R y
  rw [IsLocalization.AtPrime.ringKrullDim_eq_height y.asIdeal ((Spec R).presheaf.stalk y),
     Ideal.height_eq_primeHeight y.asIdeal, Ideal.primeHeight]
  apply WithBot.coe_eq_coe.mpr
  congr
  ext
  simp only [PrimeSpectrum.instPartialOrder, PartialOrder.lift, PrimeSpectrum.le_iff_specializes,
    OrderDual.instPreorder, OrderDual.instLE]
  exact Eq.to_iff rfl

end AlgebraicGeometry
