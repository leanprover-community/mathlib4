/-
Copyright (c) 2024 Sophie Morel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patricio Gallardo Candela, Johan Commelin, Yun Liu, Sophie Morel, David Swinarski,
Weihong Xu
-/
import Mathlib.AlgebraicGeometry.Grassmannian.Lemmas
import Mathlib.AlgebraicGeometry.Gluing
import Mathlib.AlgebraicGeometry.Pullbacks
import Mathlib.LinearAlgebra.Matrix.Basis
import Mathlib.LinearAlgebra.Matrix.NonsingularInverse
import Mathlib.AlgebraicGeometry.OpenImmersion
import Mathlib.RingTheory.TensorProduct.Basic
import Mathlib.RingTheory.Localization.LocalizationLocalization

/-!

# The Grassmannian scheme

The goal of this file is to define the Grassmannian scheme by gluing affine charts.
We fix a commutative ring `K`, a free `K`-module of finite type `V` and two natural numbers
`r` and `c`. The scheme we define should parametrize surjective `K`-linear maps
`V â†’â‚—[K] (Fin r â†’ K)`, assuming that `V` is of rank `r + c`. We actually define the scheme
without assuming the condition on the rank of `V`, but it is empty unless the rank of `V` is
`r + c`.

Main definitions:
* `Grassmannian.glueData K V r c`: the `AlgebraicGeometry.Scheme.GlueData` defining the
Grassmannian scheme.
* `Grassmannian K V r c`: the Grassmannian scheme, defined as
`(Grassmannian.glueData K V r c).glued`.
* `Grassmannian.structMorphism K V r c`: the structure morphism from `Grassmannian K V r c`
to `Spec K`.

# Implementation

We use as index type for the charts the type `Basis (Fin (r + c)) K V` (so this is empty unless
`V` is free of rank `r + c`). All the charts are the same and equal to the affine space with
coordinates indexed by `Fin c Ã— Fin r`, that is, to `Spec (MvPolynomial (Fin c Ã— Fin r) K)`.
The idea is that, for `i` in `Basis (Fin (r + c)) K V`, the corresponding chart will
parametrize all surjective `K`-linear maps `Ï† : V â†’â‚—[K] (Fin r â†’ K)` that become isomorphisms
when restricted to the `K`-submodule generated by the first `r` vectors of the basis `i`.
To get the point of the chart corresponding to `Ï†`, we take the matrix of `Ï†` in the basis `i` of
`V` and the canonical basis of `Fin r â†’ K`, we divide it on the right by its top `r Ã— r`
square submatrix (which is invertible by assumption), and we taken the botton `c Ã— r`
submatrix.

This is almost the usual description of the Grassmannian by charts, with three differences:
* We consider the Grassmannian parametrizing `r`-dimensional quotients of `V` instead of
`r`-dimensional subspaces of `V`, because this is more natural when working over a general
ring (or scheme).
* In the usual description, we fix a basis of `V` and index the chart by its subsets `I` of
cardinality `r`. Here, to avoid making a choice, we instead index charts by the set of
bases of `V` and always choose the subset `I` to consist of the first `r` vectors.
* Instead of working with `FiniteDimensional.finrank K V - r`, which would cause technical
trouble because of the way subtraction works on `â„•`, we introduce the codimension `c` as an
auxiliary variable, and our constructions are only interesting when `r + c` is equal to
`FiniteDimensional.finrank K V`.

# TODO

Describe the functor of points of the Grassmannian scheme. (We need more about quasi-coherent
sheaves in mathlib before we can do that.)

# Notes.

This contribution was created as part of the AIM workshop "Formalizing algebraic geometry" in
June 2024.
-/

open AlgebraicGeometry Scheme FiniteDimensional CategoryTheory Matrix

noncomputable section

universe u v w

variable (K V : Type u) [CommRing K] [AddCommGroup V] [Module K V]
variable (r c : â„•)
variable {A : Type*} [CommRing A] [Algebra K A]

namespace Grassmannian

/-- The `(r + c) Ã— r` matrix with ceofficients in `MvPolynomial (Fin c Ã— Fin r) K` obtained by
stacking the `r Ã— r` identity matrix on top of the `c Ã— r` matrix of indeterminates.
-/
abbrev matrix_I_X : Matrix (Fin (r + c)) (Fin r) (MvPolynomial (Fin c Ã— Fin r) K) :=
  Matrix.of
    (fun x y â†¦
      if h : x < r then if x.1 = y.1 then 1 else 0
      else MvPolynomial.X (âŸ¨x.1 - r, by have := x.2; rw [lt_iff_not_le, not_not] at h; omegaâŸ©, y))

/-- The `c Ã— r` matrix of indeterminates in `MvPolynomial (Fin c Ã— Fin r) K`.
-/
abbrev matrix_X : Matrix (Fin c) (Fin r) (MvPolynomial (Fin c Ã— Fin r) K) :=
  Matrix.of (fun p q â†¦ MvPolynomial.X (p,q))

lemma matrix_I_X_submatrixâ‚ :
    (matrix_I_X K r c).submatrix (Fin.castLE (Nat.le_add_right r c)) id = 1 := by
  apply Matrix.ext; intro p q
  simp only [submatrix_apply, id_eq, of_apply, Fin.coe_castLE, Fin.is_lt, â†“reduceDIte]
  by_cases h : p = q
  Â· simp only [h, â†“reduceIte]; rw [h, one_apply_eq]
  Â· simp only [ne_eq, h, not_false_eq_true, one_apply_ne, ite_eq_right_iff, one_ne_zero, imp_false]
    rw [â† Fin.ext_iff]; intro h'; exfalso; exact h h'

lemma matrix_I_X_submatrixâ‚‚ : (matrix_I_X K r c).submatrix
    (fun i â†¦ âŸ¨i.1 + r, by have := i.2; omegaâŸ© : Fin c â†’ Fin (r + c)) id
    = matrix_X K r c := by
  apply Matrix.ext; intro p q
  simp only [submatrix_apply, id_eq, of_apply, add_lt_iff_neg_right, not_lt_zero', â†“reduceDIte,
    add_tsub_cancel_right, Fin.eta]

variable {K V}

/-- Take the `(r + c) Ã— r` matrix with ceofficients in `MvPolynomial (Fin c Ã— Fin r) K` obtained by
stacking the `r Ã— r` identity matrix on top of the `c Ã— r` matrix of indeterminates, multiply on
the left by the change of basis matrix from `j` to `i`, take the top `r Ã— r` submatrix.
-/
abbrev matrix_F (i j : Basis (Fin (r + c)) K V) :
    Matrix (Fin r) (Fin r) (MvPolynomial (Fin c Ã— Fin r) K) :=
  Matrix.submatrix ((j.toMatrix i).map (algebraMap K _) * matrix_I_X K r c)
  (Fin.castLE (Nat.le_add_right r c)) id

/-- Take the `(r + c) Ã— r` matrix with ceofficients in `MvPolynomial (Fin c Ã— Fin r) K` obtained by
stacking the `r Ã— r` identity matrix on top of the `c Ã— r` matrix of indeterminates, multiply on
the left by the change of basis matrix from `j` to `i`, take the bottom `c Ã— r` submatrix.
After multiplying on the right by the inverse of the `matrix_F i j` of the previous definition,
we get a matrix whose entries are the images of the indeterminates by the transition map
`t i j` in `Grassmannian.glueData`.
-/
abbrev matrix_G (i j : Basis (Fin (r + c)) K V) :
    Matrix (Fin c) (Fin r) (MvPolynomial (Fin c Ã— Fin r) K) :=
  Matrix.submatrix ((j.toMatrix i).map (algebraMap K _) * matrix_I_X K r c)
    (fun i â†¦ âŸ¨i.1 + r, by have := i.2; omegaâŸ©) id

lemma matrix_F_eq_id_of_diagonal (i : Basis (Fin (r + c)) K V) :
    matrix_F r c i i = 1 := by
  apply Matrix.ext; intro a b
  simp only [matrix_F, Basis.toMatrix_self, MvPolynomial.algebraMap_eq, map_zero, _root_.map_one,
    Matrix.map_one, Matrix.one_mul, submatrix_apply, id_eq, of_apply, Fin.coe_castLE, Fin.is_lt,
    â†“reduceDIte]
  by_cases h : a = b
  Â· simp only [h, â†“reduceIte]; rw [h, one_apply_eq]
  Â· simp only [ne_eq, h, not_false_eq_true, one_apply_ne, ite_eq_right_iff]
    rw [â† Fin.ext_iff]
    simp only [h, false_implies]

lemma matrix_G_eq_X_of_diagonal (i : Basis (Fin (r + c)) K V) :
    matrix_G r c i i = matrix_X K r c := by
  apply Matrix.ext; intro _ _
  simp only [matrix_G, Basis.toMatrix_self, MvPolynomial.algebraMap_eq, map_zero, _root_.map_one,
    Matrix.map_one, Matrix.one_mul, submatrix_apply, id_eq, of_apply, add_lt_iff_neg_right,
    not_lt_zero', â†“reduceDIte, add_tsub_cancel_right, Fin.eta]

/-- If `i` and `j` are in the type indexing charts, this is the equation of the intersection of
the charts `U i` and `U j`, seen as a basic open subscheme of `U i`.
-/
def equation (i j : Basis (Fin (r + c)) K V) :
    (MvPolynomial ((Fin c) Ã— Fin r) K) := (matrix_F r c i j).det

/-- Take the `(r + c) Ã— r` matrix with ceofficients in `MvPolynomial (Fin c Ã— Fin r) K` obtained by
stacking the `r Ã— r` identity matrix on top of the `c Ã— r` matrix of indeterminates, multiply on
the left by the change of basis matrix from `j` to `i`, take the top `r Ã— r` submatrix, and see
the result as a matrix with entries in the localization of `MvPolynomial (Fin (c Ã— r)) K`
defining the "intersection of charts" `V i j`.
-/
abbrev matrix_F' (i j : Basis (Fin (r + c)) K V) :=
  (algebraMap (MvPolynomial (Fin c Ã— Fin r) K)
    (Localization.Away (equation r c i j))).mapMatrix (matrix_F r c i j)

/-- Take the `(r + c) Ã— r` matrix with ceofficients in `MvPolynomial (Fin c Ã— Fin r) K` obtained by
stacking the `r Ã— r` identity matrix on top of the `c Ã— r` matrix of indeterminates, multiply on
the left by the change of basis matrix from `j` to `i`, take the bottom `c Ã— r` submatrix, and see
the result as a matrix with entries in the localization of `MvPolynomial (Fin (c Ã— r)) K`
defining the "intersection of charts" `V i j`.
-/
abbrev matrix_G' (i j : Basis (Fin (r + c)) K V) :=
  (matrix_G r c i j).map
  (algebraMap (MvPolynomial (Fin c Ã— Fin r) K) (Localization.Away (equation r c i j)))

/-- The `c Ã— r` matrix of indeterminates in `MvPolynomial (Fin c Ã— Fin r) K`,
seen as a matrix with entries in the localization of `MvPolynomial (Fin (c Ã— r)) K`
defining the "intersection of charts" `V i j`.
-/
abbrev matrix_X' (i j : Basis (Fin (r + c)) K V) :=
  (matrix_X K r c).map
  (algebraMap (MvPolynomial (Fin c Ã— Fin r) K) (Localization.Away (equation r c i j)))

lemma matrix_G'_eq_X_of_diagonal (i : Basis (Fin (r + c)) K V) :
    matrix_G' r c i i = Matrix.of (fun p q â†¦
    (algebraMap (MvPolynomial (Fin c Ã— Fin r) K)
    (Localization.Away (equation r c i i)) (MvPolynomial.X (p,q)))) := by
  simp only [matrix_G', matrix_G_eq_X_of_diagonal]
  ext _ _
  simp only [map_apply, of_apply]

lemma matrix_F'_isUnit (i j : Basis (Fin (r + c)) K V) :
    IsUnit (matrix_F' r c i j) := by
    rw [Matrix.isUnit_iff_isUnit_det]
    rw [â† RingHom.map_det]
    simp only [equation]
    refine isUnit_iff_exists_inv.mpr ?_
    existsi IsLocalization.Away.invSelf (matrix_F r c i j).det
    simp only [IsLocalization.Away.mul_invSelf]

/-- The open immersion from `V i j` (the "intersection" of the charts `U i` and `U j`) into
the chart `U i`.
-/
abbrev open_immersion (i j : Basis (Fin (r + c)) K V) :=
  Spec.map (CommRingCat.ofHom (algebraMap (MvPolynomial ((Fin c) Ã— Fin r) K)
    (Localization.Away (equation r c i j))))

/-- Auxiliary function to defined the transition map `t i j` between `V i j` and `V j i`.-/
abbrev transition_aux (i j : Basis (Fin (r + c)) K V) :
    MvPolynomial (Fin c Ã— Fin r) K â†’â‚[K] Localization.Away (equation r c i j) :=
  {MvPolynomial.evalâ‚‚Hom (algebraMap K (Localization.Away (equation r c i j)))
  (fun pq â†¦ ((matrix_G' r c i j) * (matrix_F' r c i j)â»Â¹) pq.1 pq.2) with
    commutes' := by
      intro a
      simp only [RingHom.toMonoidHom_eq_coe, MvPolynomial.algebraMap_eq, OneHom.toFun_eq_coe,
        MonoidHom.toOneHom_coe, MonoidHom.coe_coe, MvPolynomial.evalâ‚‚Hom_C]}

lemma transition_aux_matrix_X (i j : Basis (Fin (r + c)) K V) :
    Matrix.map (matrix_X K r c) (transition_aux r c i j) =
    (matrix_G' r c i j) * (matrix_F' r c i j)â»Â¹ := by
  conv_rhs => congr; change matrix_G' r c i j
  apply Matrix.ext; intro _ _
  simp only [AlgHom.coe_mk, RingHom.mapMatrix_apply, MvPolynomial.coe_evalâ‚‚Hom, map_apply,
      of_apply, MvPolynomial.evalâ‚‚_X]

lemma transition_aux_matrix_I_X (i j : Basis (Fin (r + c)) K V) :
    (matrix_I_X K r c).map (transition_aux r c i j) =
    ((j.toMatrix i).map (algebraMap K _) * matrix_I_X K r c).map
    (algebraMap (MvPolynomial (Fin c Ã— Fin r) K)
    (Localization.Away (equation r c i j))) * (matrix_F' r c i j)â»Â¹ := by
  refine Matrix.eq_of_submatrix_eq (Fin.castLE (Nat.le_add_right r c))
    (fun i â†¦ âŸ¨i.1 + r, by omegaâŸ© : Fin c â†’ Fin (r + c))
    _ _ ?_ ?_ ?_
  Â· rw [Matrix.submatrix_mul _ _ (Fin.castLE (Nat.le_add_right r c)) id id Function.bijective_id,
      Matrix.submatrix_id_id, Matrix.submatrix_map, matrix_I_X_submatrixâ‚]
    rw [Matrix.submatrix_map]
    conv_rhs => congr; change matrix_F' r c i j
    have := IsUnit.invertible (matrix_F'_isUnit r c i j)
    rw [Matrix.mul_inv_of_invertible]
    simp only [AlgHom.coe_mk, RingHom.mapMatrix_apply, MvPolynomial.coe_evalâ‚‚Hom,
      MvPolynomial.evalâ‚‚_zero, MvPolynomial.evalâ‚‚_one, Matrix.map_one]
  Â· rw [Matrix.submatrix_mul _ _ _ id id Function.bijective_id,
      Matrix.submatrix_id_id, Matrix.submatrix_map, matrix_I_X_submatrixâ‚‚, Matrix.submatrix_map]
    exact transition_aux_matrix_X r c _ _
  Â· intro i
    by_cases h : i.1 < r
    Â· left
      existsi âŸ¨i.1, hâŸ©
      simp only [Fin.castLE_mk, Fin.eta]
    Â· right
      existsi âŸ¨i - r, by omegaâŸ©
      simp only; rw [Fin.ext_iff]
      rw [lt_iff_not_le, not_not] at h
      simp only [h, Nat.sub_add_cancel]

lemma transition_aux_matrix (i j k l : Basis (Fin (r + c)) K V) :
    ((l.toMatrix k).map (algebraMap K _) * matrix_I_X K r c).map
    (transition_aux r c i j) =
    (l.toMatrix k).map (algebraMap K (Localization.Away (equation r c i j))) *
    (j.toMatrix i).map (algebraMap K (Localization.Away (equation r c i j))) *
    (matrix_I_X K r c).map (algebraMap (MvPolynomial (Fin c Ã— Fin r) K)
    (Localization.Away (equation r c i j))) * (matrix_F' r c i j)â»Â¹ := by
  erw [RingHom.map_matrix_mul']; rw [Matrix.map_map]
  have : (transition_aux r c i j).toRingHom.toFun âˆ˜
      (algebraMap K (MvPolynomial (Fin c Ã— Fin r) K)) = algebraMap K _ := by
    ext _
    simp only [RingHom.mapMatrix_apply, RingHom.toMonoidHom_eq_coe, OneHom.toFun_eq_coe,
      MonoidHom.toOneHom_coe, MonoidHom.coe_coe, MvPolynomial.coe_evalâ‚‚Hom,
      MvPolynomial.algebraMap_eq, Function.comp_apply, MvPolynomial.evalâ‚‚_C]
  rw [this]
  conv_lhs => congr; rfl
              change (matrix_I_X K r c).map â‡‘(transition_aux r c i j)
              rw [transition_aux_matrix_I_X]
  conv_rhs => rw [Matrix.mul_assoc, Matrix.mul_assoc]
  congr 1
  conv_rhs => rw [â† Matrix.mul_assoc]
  congr 1
  conv_lhs => rw [RingHom.map_matrix_mul']
  rfl

lemma transition_aux_matrix_F (i j k : Basis (Fin (r + c)) K V) :
    Matrix.map (matrix_F r c j k) (transition_aux r c i j) =
    (matrix_F r c i k).map (algebraMap (MvPolynomial (Fin c Ã— Fin r) K)
    (Localization.Away (equation r c i j))) * (matrix_F' r c i j)â»Â¹ := by
  simp only [matrix_F]
  rw [â† Matrix.submatrix_map, transition_aux_matrix]
  erw [â† RingHom.map_matrix_mul']; rw [Basis.toMatrix_mul_toMatrix k j i]
  rw [Matrix.submatrix_mul _ _ (eâ‚ := (Fin.castLE (Nat.le_add_right r c)))
    (eâ‚‚ := id (Î± := Fin r)) _ Function.bijective_id, Matrix.submatrix_id_id]
  congr 1
  rw [â† Matrix.submatrix_map]
  conv_rhs => rw [RingHom.map_matrix_mul']
  rfl

lemma transition_aux_matrix_G (i j k : Basis (Fin (r + c)) K V) :
    Matrix.map (matrix_G r c j k) (transition_aux r c i j) =
    (matrix_G r c i k).map (algebraMap (MvPolynomial (Fin c Ã— Fin r) K)
    (Localization.Away (equation r c i j))) * (matrix_F' r c i j)â»Â¹ := by
  simp only [matrix_G]
  rw [â† Matrix.submatrix_map, transition_aux_matrix]
  erw [â† RingHom.map_matrix_mul']; rw [Basis.toMatrix_mul_toMatrix k j i]
  conv_lhs => rw [Matrix.submatrix_mul _ (matrix_F' r c i j)â»Â¹ _ (eâ‚‚ := id (Î± := Fin r))
    (eâ‚ƒ := id (Î± := Fin r)) Function.bijective_id, Matrix.submatrix_id_id]
  congr 1
  rw [â† Matrix.submatrix_map]
  conv_rhs => rw [RingHom.map_matrix_mul']
  rfl

lemma transition_aux_equation (i j k : Basis (Fin (r + c)) K V) :
    transition_aux r c i j (equation r c j k) =
    algebraMap _ (Localization.Away (equation r c i j)) (equation r c i k) *
    IsLocalization.Away.invSelf (equation r c i j) := by
  simp only [equation]
  conv_lhs => erw [RingHom.map_det (transition_aux r c i j).toRingHom (matrix_F r c j k)]
              rw [RingHom.mapMatrix_apply]
              change ((matrix_F r c j k).map â‡‘(transition_aux r c i j)).det
              rw [transition_aux_matrix_F, Matrix.det_mul, det_nonsing_inv, matrix_F',
                â† RingHom.map_det, â† RingHom.mapMatrix_apply, â† RingHom.map_det]
  congr 1
  rw [â† one_mul (Ring.inverse _)]
  symm
  simp only [equation]
  rw [Ring.eq_mul_inverse_iff_mul_eq, mul_comm, IsLocalization.Away.mul_invSelf]
  exact IsLocalization.Away.map_unit _

lemma transition_aux_equation_flip (i j : Basis (Fin (r + c)) K V) :
    transition_aux r c i j (equation r c j i) = IsLocalization.Away.invSelf (equation r c i j) := by
  simp only [equation]
  conv_lhs => erw [RingHom.map_det (transition_aux r c i j).toRingHom (matrix_F r c j i)]
              rw [RingHom.mapMatrix_apply]
  refine Eq.trans (b := (matrix_F' r c i j)â»Â¹.det) ?_ ?_
  Â· change ((matrix_F r c j i).map â‡‘(transition_aux r c i j)).det = _
    rw [transition_aux_matrix_F, matrix_F_eq_id_of_diagonal]
    simp only [map_zero, _root_.map_one, Matrix.map_one, RingHom.mapMatrix_apply, one_mul]
  Â· rw [det_nonsing_inv, matrix_F', â† RingHom.map_det]
    exact (IsLocalization.Away.invSelf_eq (equation r c i j)).symm

lemma transition_aux_equation_isUnit (i j : Basis (Fin (r + c)) K V) :
    IsUnit ((transition_aux r c i j) (equation r c j i)) := by
  rw [transition_aux_equation_flip]
  exact IsLocalization.Away.invSelf_unit (equation r c i j)

/-- The action on functions of the transition map `t i j` between `V i j` and `V j i`, seen as
an `AlgHom`.
-/
abbrev transition (i j : Basis (Fin (r + c)) K V) :
    Localization.Away (equation r c j i) â†’â‚[K] Localization.Away (equation r c i j) :=
  {
   Localization.awayLift (transition_aux r c i j) (equation r c j i)
    (transition_aux_equation_isUnit _ _ _ _) (P := Localization.Away (equation r c i j)) with
   commutes' := by
     intro x
     dsimp
     rw [IsScalarTower.algebraMap_apply _ (MvPolynomial (Fin c Ã— Fin r) K) _]
     rw [IsLocalization.Away.AwayMap.lift_eq]
     simp only [AlgHom.coe_ringHom_mk, RingHom.mapMatrix_apply, MvPolynomial.algebraMap_eq,
       MvPolynomial.evalâ‚‚Hom_C]
  }

lemma transition_F'_flip (i j : Basis (Fin (r + c)) K V) :
    Matrix.map (matrix_F' r c j i) (transition r c i j) = (matrix_F' r c i j)â»Â¹ := by
  rw [transition, matrix_F', RingHom.mapMatrix_apply, Matrix.map_map]
  rw [â† AlgHom.coe_toRingHom]
  rw [â† RingHom.coe_comp]
  change (matrix_F r c j i).map ((transition r c i j).toRingHom.comp (algebraMap _ _)) = _
  rw [IsLocalization.Away.AwayMap.lift_comp]
  change (matrix_F r c j i).map â‡‘(transition_aux r c i j) = _
  rw [transition_aux_matrix_F]; rw [matrix_F_eq_id_of_diagonal]
  simp only [map_zero, _root_.map_one, Matrix.map_one, RingHom.mapMatrix_apply, one_mul]

lemma transition_F'_inv_flip (i j : Basis (Fin (r + c)) K V) :
    Matrix.map (matrix_F' r c j i)â»Â¹ (transition r c i j) = matrix_F' r c i j := by
  rw [â† AlgHom.coe_toRingHom]
  erw [â† AlgHom.mapMatrix_apply]
  rw [Matrix.nonsing_inv_eq_ring_inverse, AlgHom.map_inv, AlgHom.mapMatrix_apply,
    transition_F'_flip, â† Matrix.nonsing_inv_eq_ring_inverse (matrix_F' r c i j)â»Â¹,
    Matrix.nonsing_inv_nonsing_inv]
  rw [â† Matrix.isUnit_iff_isUnit_det]
  all_goals (exact matrix_F'_isUnit _ _ _ _)

lemma transition_transition_flip (i j : Basis (Fin (r + c)) K V) :
    (transition r c i j).comp (transition r c j i) = AlgHom.id _ _ := by
  apply AlgHom.coe_ringHom_injective
  apply IsLocalization.ringHom_ext (R := MvPolynomial (Fin c Ã— Fin r) K)
    (M := Submonoid.powers (equation r c i j))
  simp only [AlgHom.id_toRingHom, RingHomCompTriple.comp_eq]
  rw [AlgHom.comp_toRingHom, RingHom.comp_assoc]
  conv_lhs => congr; rfl; rw [transition]
              change (transition r c j i).toRingHom.comp (algebraMap _ _)
              rw [IsLocalization.Away.AwayMap.lift_comp]
  ext x
  Â· rw [â† MvPolynomial.algebraMap_eq]
    rw [â† AlgHom.comp_toRingHom]
    erw [AlgHom.comp_algebraMap]
    simp only [transition, AlgHom.coe_ringHom_mk, RingHom.mapMatrix_apply,
    MvPolynomial.comp_evalâ‚‚Hom, RingHom.coe_comp, MvPolynomial.coe_evalâ‚‚Hom, Function.comp_apply,
    MvPolynomial.evalâ‚‚_C]
    rw [â† IsScalarTower.algebraMap_apply]
  Â· simp only [AlgHom.coe_ringHom_mk, RingHom.mapMatrix_apply, MvPolynomial.comp_evalâ‚‚Hom,
    MvPolynomial.evalâ‚‚Hom_X']
    change ((matrix_G' r c j i) * (matrix_F' r c j i)â»Â¹).map (transition r c i j) x.1 x.2 = _
    erw [Matrix.map_mul, transition_F'_inv_flip]
    conv_lhs =>
      rw [transition, matrix_G', Matrix.map_map, â† RingHom.coe_comp]
      change (((matrix_G r c j i).map ((transition r c i j).toRingHom.comp (algebraMap _ _))) *
        (matrix_F' r c i j)) x.1 x.2
      rw [IsLocalization.Away.AwayMap.lift_comp]
      change ((matrix_G r c j i).map â‡‘(transition_aux r c i j) * matrix_F' r c i j) x.1 x.2
      rw [transition_aux_matrix_G, matrix_G_eq_X_of_diagonal]
    rw [Matrix.mul_assoc, Matrix.nonsing_inv_mul, Matrix.mul_one]
    simp only [map_apply, of_apply, Prod.mk.eta]
    rw [â† Matrix.isUnit_iff_isUnit_det]; exact matrix_F'_isUnit r c i j

/--
The transition map `t i j` from `V i j` to `V j i`.
-/
abbrev transition_Spec (i j : Basis (Fin (r + c)) K V) :=
  Spec.map (CommRingCat.ofHom (transition r c i j).toRingHom)

lemma transition_transition_flip_Spec (i j : Basis (Fin (r + c)) K V) :
    transition_Spec r c i j â‰« transition_Spec r c j i = ðŸ™ _ := by
  simp only [transition_Spec]
  rw [â† Spec.map_comp]
  change Spec.map (CommRingCat.ofHom ((transition r c i j).comp (transition r c j i)).toRingHom) = _
  rw [transition_transition_flip]
  change Spec.map (ðŸ™ _) = _
  rw [Spec.map_id]

/-- The `RingHom` version of the action on functions of the gluing morphism from `V j k`
(morally, the intersection of the charts `U j` and `U k`, seen as a subscheme of `U j`) and the
fiber product of `V i j` and `V i k` over `U i`.-/
def transition'â‚RingHom (i j k : Basis (Fin (r + c)) K V) :
    Localization.Away (equation r c j k) â†’+* (TensorProduct (MvPolynomial
    (Fin c Ã— Fin r) K) (Localization.Away (equation r c i j))
    (Localization.Away (equation r c i k))) := by
  refine IsLocalization.Away.lift (equation r c j k) ?_ (S := Localization.Away (equation r c j k))
    (g := Algebra.TensorProduct.includeLeftRingHom.comp (transition_aux r c i j))
    (P := TensorProduct (MvPolynomial (Fin c Ã— Fin r) K) (Localization.Away (equation r c i j))
    (Localization.Away (equation r c i k)))
  rw [RingHom.comp_apply]
  change IsUnit (Algebra.TensorProduct.includeLeftRingHom ((transition_aux r c i j)
    (equation r c j k)))
  rw [transition_aux_equation, RingHom.map_mul, IsUnit.mul_iff (M := TensorProduct (MvPolynomial
    (Fin c Ã— Fin r) K) (Localization.Away (equation r c i j)) (Localization.Away
    (equation r c i k)))]
  constructor
  Â· rw [â† RingHom.comp_apply, Algebra.TensorProduct.includeLeftRingHom_comp_algebraMap,
      RingHom.comp_apply]
    exact IsUnit.map Algebra.TensorProduct.includeRight (IsLocalization.Away.map_unit _)
  Â· exact IsUnit.map Algebra.TensorProduct.includeLeftRingHom
      (IsLocalization.Away.invSelf_unit _)

/-- The `AlgHom` version of the action on functions of the gluing morphism from `V j k`
(morally, the intersection of the charts `U j` and `U k`, seen as a subscheme of `U j`) and the
fiber product of `V i j` and `V i k` over `U i`.-/
def transition'â‚ (i j k : Basis (Fin (r + c)) K V) :
    Localization.Away (equation r c j k) â†’â‚[K] (TensorProduct (MvPolynomial
    (Fin c Ã— Fin r) K) (Localization.Away (equation r c i j))
    (Localization.Away (equation r c i k))) := by
  exact
  {
    transition'â‚RingHom r c i j k with
    commutes' := by
      intro x
      simp only [transition'â‚RingHom, AlgHom.coe_ringHom_mk, RingHom.mapMatrix_apply,
        MvPolynomial.comp_evalâ‚‚Hom, Algebra.TensorProduct.includeLeftRingHom_apply,
        RingHom.toMonoidHom_eq_coe, OneHom.toFun_eq_coe, MonoidHom.toOneHom_coe, MonoidHom.coe_coe,
        Algebra.TensorProduct.algebraMap_apply]
      rw [IsScalarTower.algebraMap_apply (R := K) (S := MvPolynomial (Fin c Ã— Fin r) K)]
      rw [IsLocalization.Away.AwayMap.lift_eq]
      simp only [MvPolynomial.algebraMap_eq, MvPolynomial.evalâ‚‚Hom_C, RingHom.coe_comp,
        Function.comp_apply, Algebra.TensorProduct.includeLeftRingHom_apply]
  }

/-- The gluing morphism from `V j k` (morally, the intersection of the charts `U j` and `U k`,
seen as a subscheme of `U j`) and the fiber product of `V i j` and `V i k` over `U i`.-/
def transition'â‚_Spec (i j k : Basis (Fin (r + c)) K V) :
    Limits.pullback (open_immersion r c i j) (open_immersion r c i k) âŸ¶
    Spec (CommRingCat.of (Localization.Away (equation r c j k))) :=
  (pullbackSpecIso (MvPolynomial (Fin c Ã— Fin r) K)
    (Localization.Away (equation r c i j)) (Localization.Away (equation r c i k))).hom â‰«
    Spec.map (CommRingCat.ofHom (transition'â‚ r c i j k))

/--
The matrix `matrix_F i j`, seen as a matrix with entries in a tensor product of localizations
of `MvPolynomial (Fin (c Ã— r)) K`.
-/
abbrev matrix_F''ij (i j k : Basis (Fin (r + c)) K V) :=
  (algebraMap (MvPolynomial (Fin c Ã— Fin r) K)
  (TensorProduct (MvPolynomial (Fin c Ã— Fin r) K) (Localization.Away
  (equation r c i j)) (Localization.Away (equation r c i k)))).mapMatrix (matrix_F r c i j)

/--
The matrix `matrix_F i k`, seen as a matrix with entries in a tensor product of localizations
of `MvPolynomial (Fin (c Ã— r)) K`.
-/
abbrev matrix_F''ik (i j k : Basis (Fin (r + c)) K V) :=
  (algebraMap (MvPolynomial (Fin c Ã— Fin r) K)
  (TensorProduct (MvPolynomial (Fin c Ã— Fin r) K) (Localization.Away
  (equation r c i j)) (Localization.Away (equation r c i k)))).mapMatrix (matrix_F r c i k)

lemma matrix_F''ij_inv_eq (i j k : Basis (Fin (r + c)) K V) :
    (matrix_F''ij r c i j k)â»Â¹ =
    RingHom.mapMatrix Algebra.TensorProduct.includeLeftRingHom (matrix_F' r c i j)â»Â¹ := by
  conv_rhs => rw [Matrix.nonsing_inv_eq_ring_inverse]
              change AlgHom.mapMatrix (R := MvPolynomial (Fin c Ã— Fin r) K)
                Algebra.TensorProduct.includeLeft (Ring.inverse (matrix_F' r c i j))
              rw [AlgHom.map_inv (f := Algebra.TensorProduct.includeLeft.mapMatrix)
              (Î± := Matrix (Fin r) (Fin r) (Localization.Away (equation r c i j)))
              (Î² := Matrix (Fin r) (Fin r) (TensorProduct (MvPolynomial
              (Fin c Ã— Fin r) K) (Localization.Away (equation r c i j))
              (Localization.Away (equation r c i k)))) _ (matrix_F'_isUnit r c i j)]
  rw [Matrix.nonsing_inv_eq_ring_inverse, matrix_F']
  conv_rhs => rw [AlgHom.mapMatrix_apply, RingHom.mapMatrix_apply, Matrix.map_map]
              erw [â† RingHom.coe_comp, â† Algebra.TensorProduct.algebraMap]
              rw [â† RingHom.mapMatrix_apply]

lemma matrix_F''ik_inv_eq (i j k : Basis (Fin (r + c)) K V) :
    (matrix_F''ik r c i j k)â»Â¹ =
    AlgHom.mapMatrix Algebra.TensorProduct.includeRight (matrix_F' r c i k)â»Â¹ := by
  conv_rhs => rw [Matrix.nonsing_inv_eq_ring_inverse]
              rw [AlgHom.map_inv (f := Algebra.TensorProduct.includeRight.mapMatrix)
              (Î± := Matrix (Fin r) (Fin r) (Localization.Away (equation r c i k)))
              (R := MvPolynomial (Fin c Ã— Fin r) K)
              (Î² := Matrix (Fin r) (Fin r) (TensorProduct (MvPolynomial
              (Fin c Ã— Fin r) K) (Localization.Away (equation r c i j))
              (Localization.Away (equation r c i k)))) _ (matrix_F'_isUnit r c i k)]
  rw [Matrix.nonsing_inv_eq_ring_inverse, matrix_F']
  have heq : Algebra.TensorProduct.includeRight âˆ˜ (algebraMap (MvPolynomial
    (Fin c Ã— Fin r) K) (Localization.Away (equation r c i k))) =
    algebraMap _ (TensorProduct (MvPolynomial
    (Fin c Ã— Fin r) K) (Localization.Away (equation r c i j))
    (Localization.Away (equation r c i k))) := by
    ext _
    simp only [Function.comp_apply, AlgHom.commutes, Algebra.TensorProduct.algebraMap_apply]
  rw [AlgHom.mapMatrix_apply, RingHom.mapMatrix_apply, Matrix.map_map, heq]
  rw [matrix_F''ik, RingHom.mapMatrix_apply]

/--
The matrix `matrix_G i k`, seen as a matrix with entries in a tensor product of localizations
of `MvPolynomial (Fin (c Ã— r)) K`.
-/
abbrev matrix_G''ik (i j k : Basis (Fin (r + c)) K V) :=
  (matrix_G r c i k).map
  (algebraMap (MvPolynomial (Fin c Ã— Fin r) K)
  (TensorProduct (MvPolynomial (Fin c Ã— Fin r) K) (Localization.Away
  (equation r c i j)) (Localization.Away (equation r c i k))))

lemma matrix_F''ij_isUnit (i j k : Basis (Fin (r + c)) K V) :
    IsUnit (matrix_F''ij r c i j k) := by
  rw [matrix_F''ij, Algebra.TensorProduct.algebraMap, â† RingHom.mapMatrix_comp, RingHom.comp_apply]
  apply IsUnit.map
  exact matrix_F'_isUnit r c i j

lemma transition'â‚_F' (i j k : Basis (Fin (r + c)) K V) :
    (matrix_F' r c j k).map (transition'â‚ r c i j k) =
    (matrix_F''ik r c i j k) * (matrix_F''ij r c i j k)â»Â¹ := by
  rw [matrix_F', transition'â‚];
  conv_lhs => change ((algebraMap _ (Localization.Away (equation r c j k))).mapMatrix
                (matrix_F r c j k)).map (transition'â‚RingHom r c i j k)
  rw [â† RingHom.mapMatrix_apply]
  conv_lhs => change (transition'â‚RingHom r c i j k).mapMatrix
                ((algebraMap (MvPolynomial (Fin c Ã— Fin r) K) _).mapMatrix (matrix_F r c j k))
              rw [RingHom.mapMatrix_apply, RingHom.mapMatrix_apply, Matrix.map_map,
              â† RingHom.coe_comp, transition'â‚RingHom, IsLocalization.Away.AwayMap.lift_comp,
              RingHom.coe_comp, â† Matrix.map_map]
              change ((matrix_F r c j k).map (transition_aux r c i j)).map
                Algebra.TensorProduct.includeLeftRingHom
              rw [transition_aux_matrix_F]
              rw [RingHom.map_matrix_mul']
              congr; rw [Matrix.map_map]
              change (matrix_F r c i k).map (â‡‘(Algebra.TensorProduct.includeLeftRingHom) âˆ˜
    (algebraMap (MvPolynomial (Fin c Ã— Fin r) K) (Localization.Away (equation r c i j))))
              rw [â† RingHom.coe_comp]
              rw [â† Algebra.TensorProduct.algebraMap]; rfl
              change (matrix_F' r c i j)â»Â¹.map â‡‘(Algebra.TensorProduct.includeLeftRingHom)
              rw [â† RingHom.mapMatrix_apply, â† matrix_F''ij_inv_eq]
  rfl

lemma transition'â‚_F'_inv (i j k : Basis (Fin (r + c)) K V) :
    (matrix_F' r c j k)â»Â¹.map (transition'â‚ r c i j k) =
    (matrix_F''ij r c i j k) * (matrix_F''ik r c i j k)â»Â¹ := by
  rw [â† AlgHom.mapMatrix_apply, Matrix.nonsing_inv_eq_ring_inverse,
    AlgHom.map_inv (f := (transition'â‚ r c i j k).mapMatrix)
    (Î± := Matrix (Fin r) (Fin r) (Localization.Away (equation r c j k)))
    (Î² := Matrix (Fin r) (Fin r) (TensorProduct (MvPolynomial (Fin c Ã— Fin r) K)
    (Localization.Away (equation r c i j)) (Localization.Away (equation r c i k))))
    (hx := matrix_F'_isUnit r c j k), AlgHom.mapMatrix_apply, transition'â‚_F',
    â† Matrix.nonsing_inv_eq_ring_inverse ((matrix_F''ik r c i j k * (matrix_F''ij r c i j k)â»Â¹)),
    Matrix.mul_inv_rev (matrix_F''ik r c i j k), Matrix.nonsing_inv_nonsing_inv]
  exact (Matrix.isUnit_iff_isUnit_det _).mp (matrix_F''ij_isUnit r c i j k)

lemma transition'â‚_G' (i j k : Basis (Fin (r + c)) K V) :
    (matrix_G' r c j k).map (transition'â‚ r c i j k) =
    (matrix_G''ik r c i j k) * (matrix_F''ij r c i j k)â»Â¹ := by
  rw [matrix_G', transition'â‚, Matrix.map_map]
  change (matrix_G r c j k).map
    (â‡‘(transition'â‚RingHom r c i j k) âˆ˜
      â‡‘(algebraMap (MvPolynomial (Fin c Ã— Fin r) K) (Localization.Away (equation r c j k)))) = _
  rw [â† RingHom.coe_comp]
  conv_lhs => change (matrix_G r c j k).map ((transition'â‚RingHom r c i j k).comp (algebraMap
                (MvPolynomial (Fin c Ã— Fin r) K) _))
              rw [transition'â‚RingHom, IsLocalization.Away.AwayMap.lift_comp]
              rw [RingHom.coe_comp, â† Matrix.map_map]
              change ((matrix_G r c j k).map (transition_aux r c i j)).map
                Algebra.TensorProduct.includeLeftRingHom
              rw [transition_aux_matrix_G, RingHom.map_matrix_mul']
              congr; rw [Matrix.map_map]
              change (matrix_G r c i k).map (â‡‘(Algebra.TensorProduct.includeLeftRingHom) âˆ˜
    â‡‘(algebraMap (MvPolynomial (Fin c Ã— Fin r) K) (Localization.Away (equation r c i j))))
              rw [â† RingHom.coe_comp, â† Algebra.TensorProduct.algebraMap]; rfl
              change (matrix_F' r c i j)â»Â¹.map â‡‘Algebra.TensorProduct.includeLeftRingHom
              rw [â† RingHom.mapMatrix_apply, â† matrix_F''ij_inv_eq]

lemma immersion_transition'â‚_Spec (i j k : Basis (Fin (r + c)) K V) :
    transition'â‚_Spec r c i j k â‰« open_immersion r c j k =
    (Limits.pullback.fst _ _ â‰« transition_Spec r c i j) â‰« open_immersion r c j i := by
  rw [transition'â‚_Spec, transition'â‚]
  conv_lhs => congr; congr; rfl; change Spec.map (CommRingCat.ofHom (transition'â‚RingHom r c i j k))
  rw [transition'â‚RingHom]
  rw [â† cancel_epi (f := (pullbackSpecIso (MvPolynomial (Fin c Ã— Fin r) K)
    (Localization.Away (equation r c i j)) (Localization.Away (equation r c i k))).inv)]
  rw [â† Category.assoc, â† Category.assoc, Iso.inv_hom_id, Category.id_comp, â† Category.assoc]
  rw [â† Category.assoc, pullbackSpecIso_inv_fst]
  conv_rhs => rw [open_immersion, â† Spec.map_comp, â† Spec.map_comp]
  conv_lhs => rw [open_immersion, â† Spec.map_comp]
  apply congrArg Spec.map
  rw [â† CommRingCat.ringHom_comp_eq_comp, â† Category.assoc, â† CommRingCat.ringHom_comp_eq_comp]
  conv_rhs => change CommRingCat.ofHom ((transition r c i j).toRingHom.comp (algebraMap _ _)) â‰«
    CommRingCat.ofHom (algebraMap _ _)
              rw [â† CommRingCat.ringHom_comp_eq_comp]
  rw [transition]
  rw [IsLocalization.Away.AwayMap.lift_comp, IsLocalization.Away.AwayMap.lift_comp]
  rfl

lemma transition'â‚_transition (i j k : Basis (Fin (r + c)) K V) :
    transition'â‚_Spec r c i j k â‰« transition_Spec r c j k â‰« open_immersion r c k j =
    Limits.pullback.snd _ _ â‰« transition_Spec r c i k â‰« open_immersion r c k i := by
  rw [transition'â‚_Spec]
  rw [â† cancel_epi (f := (pullbackSpecIso (MvPolynomial (Fin c Ã— Fin r) K)
    (Localization.Away (equation r c i j)) (Localization.Away (equation r c i k))).inv)]
  rw [â† Category.assoc, â† Category.assoc, â† Category.assoc, Iso.inv_hom_id, Category.id_comp]
  conv_rhs => rw [â† Category.assoc, pullbackSpecIso_inv_snd, transition_Spec, open_immersion]
              rw [â† Spec.map_comp, â† Spec.map_comp, â† CommRingCat.ringHom_comp_eq_comp]
              change Spec.map (CommRingCat.ofHom ((transition r c i k).toRingHom.comp
                (algebraMap _ _)) â‰« CommRingCat.ofHom Algebra.TensorProduct.includeRight.toRingHom)
              rw [â† CommRingCat.ringHom_comp_eq_comp]
  conv_lhs => rw [open_immersion, â† Spec.map_comp, â† Spec.map_comp, â† Category.assoc,
                â† CommRingCat.ringHom_comp_eq_comp]
              congr; change CommRingCat.ofHom (f := (transition r c j k).toRingHom.comp
    (algebraMap (MvPolynomial (Fin c Ã— Fin r) K) (Localization.Away (equation r c k j)))) â‰«
                CommRingCat.ofHom â†‘(transition'â‚ r c i j k)
              rw [â† CommRingCat.ringHom_comp_eq_comp]
  apply congrArg Spec.map
  conv_lhs => congr; rfl; change (transition r c j k).toRingHom.comp (algebraMap _ _)
              rw [transition, IsLocalization.Away.AwayMap.lift_comp]
  rw [IsLocalization.Away.AwayMap.lift_comp]
  rw [â† AlgHom.comp_toRingHom]
  conv_rhs => change (Algebra.TensorProduct.includeRight.restrictScalars (R := K)).toRingHom.comp
                (transition_aux r c i k).toRingHom
              erw [â† AlgHom.comp_toRingHom (R := K)
                (Ï†â‚ := Algebra.TensorProduct.includeRight.restrictScalars (R := K))
                (Ï†â‚‚ := transition_aux r c i k)]
  rw [â† AlgHom.toRingHom_eq_coe, â† AlgHom.toRingHom_eq_coe]
  apply congrArg (fun (s : MvPolynomial (Fin c Ã— Fin r) K â†’â‚[K]
    TensorProduct (MvPolynomial (Fin c Ã— Fin r) K)
    (Localization.Away (equation r c i j)) (Localization.Away (equation r c i k))) â†¦ s.toRingHom)
  refine MvPolynomial.algHom_ext (fun pq â†¦ ?_)
  suffices h : (matrix_X K r c).map ((transition'â‚ r c i j k).comp (transition_aux r c j k)) =
      (matrix_X K r c).map ((Algebra.TensorProduct.includeRight.restrictScalars (R := K)).comp
      (transition_aux r c i k)) by
    have heq : matrix_X K r c pq.1 pq.2 = MvPolynomial.X pq := rfl
    conv_lhs => rw [â† heq, â† Matrix.map_apply (f := (transition'â‚ r c i j k).comp
                  (transition_aux r c j k)), h]
    simp only [AlgHom.coe_comp, AlgHom.coe_restrictScalars', AlgHom.coe_mk, RingHom.mapMatrix_apply,
      MvPolynomial.coe_evalâ‚‚Hom, map_apply, of_apply, Prod.mk.eta, Function.comp_apply,
      MvPolynomial.evalâ‚‚_X, Algebra.TensorProduct.includeRight_apply]
  rw [AlgHom.coe_comp, â† Matrix.map_map, transition_aux_matrix_X]
  change (matrix_G' r c j k * (matrix_F' r c j k)â»Â¹).map (transition'â‚ r c i j k).toRingHom = _
  rw [RingHom.map_matrix_mul' (matrix_G' r c j k) ((matrix_F' r c j k)â»Â¹)
    (transition'â‚ r c i j k).toRingHom]
  change (matrix_G' r c j k).map â‡‘(transition'â‚ r c i j k) *
    (matrix_F' r c j k)â»Â¹.map â‡‘(transition'â‚ r c i j k) = _
  rw [transition'â‚_G', transition'â‚_F'_inv]
  rw [Matrix.mul_assoc (Î± := TensorProduct (MvPolynomial (Fin c Ã— Fin r) K) (Localization.Away
    (equation r c i j)) (Localization.Away (equation r c i k))),
    â† Matrix.mul_assoc (matrix_F''ij r c i j k)â»Â¹]
  rw [Matrix.nonsing_inv_mul _
    (by rw [â† Matrix.isUnit_iff_isUnit_det]; exact matrix_F''ij_isUnit r c i j k), Matrix.one_mul
    (Î± := TensorProduct (MvPolynomial (Fin c Ã— Fin r) K) (Localization.Away (equation r c i j))
      (Localization.Away (equation r c i k)))]
  rw [AlgHom.coe_comp, â† Matrix.map_map, transition_aux_matrix_X]; erw [RingHom.map_matrix_mul']
  have heq : ((AlgHom.restrictScalars K Algebra.TensorProduct.includeRight).toRingHom).toFun âˆ˜
    (algebraMap (MvPolynomial (Fin c Ã— Fin r) K) (Localization.Away
    (equation r c i k))) = algebraMap _ (TensorProduct (MvPolynomial
    (Fin c Ã— Fin r) K) (Localization.Away (equation r c i j))
    (Localization.Away (equation r c i k))) := by
    ext x
    simp only [AlgHom.toRingHom_eq_coe, AlgHom.coe_restrictScalars, RingHom.toMonoidHom_eq_coe,
      OneHom.toFun_eq_coe, MonoidHom.toOneHom_coe, MonoidHom.coe_coe, RingHom.coe_coe,
      Function.comp_apply, AlgHom.commutes, Algebra.TensorProduct.algebraMap_apply]
  conv_rhs => congr; rw [matrix_G', Matrix.map_map, heq]; change matrix_G''ik r c i j k
  conv_rhs => congr; rfl; erw [â† RingHom.mapMatrix_apply]
              change Algebra.TensorProduct.includeRight.mapMatrix (matrix_F' r c i k)â»Â¹
              rw [â† matrix_F''ik_inv_eq]

variable (K V)

/-- The gluing data defining the Grassmannian scheme. The type indexing the charts is
`Basis (Fin (r + c)) K V`.

If `i` is in `Basis (Fin (r + c)) K V`, then the corresponding chart `U i` is the spectrum of
the `K`-algebra of multivariable polynomials with indeterminates indexed by `Fin c Ã— Fin r`, or
in other words `Spec (MvPolynomial (Fin c Ã— Fin r) K)`.
If `i` and `j` are in `Basis (Fin (r + c)) K V`, then we define a scheme `V i j`, which is morally
the intersection of `U i` and `U j` (i.e. their fiber product over the Grassmannian), seen as
a subscheme of `U i`. We take `V i j` to be the basic open subset of `U i` defined by the
function `equation r c i j`, with the canonical morphism `f i j` to `U i`, which is an open
immersion.

If `i` and `j` are in `Basis (Fin (r + c)) K V`, we have an isomorphism `t i j` from
`V i j` to `V j i`, which we define in the following way: Consider the `c Ã— r` matrix
`matrix_X K r c`of functions on `U i` whose entries are the indeterminates in the ring
`MvPolynomial (Fin c Ã— Fin r)) K`. Let `matrix_I_X K r c` be the `(r + c) Ã— r` matrix that we
obtain by stacking the `r Ã— r` identity matrix on top of `matrix_X K r c`. Consider the map
`Ï† : V  â†’â‚—[R] (Fin r â†’ MvPolynomial (Fin c Ã— Fin r) K)` whose in the basis `i` of
`V` and the canonical basis of `Fin r â†’ MvPolynomial (Fin c Ã— Fin r K)` is `matrix_I_X K r c`.
Then the matrix of `Ï†` in the basis `j` of `V` and the canonical basis is the matrix
`matrix K r c i j` that we obtain by multiplying `matrix_I_X K r c` on the left by the change
of basis matrix `B j i` from `j` to `i`. We denote by `matrix_F K r c i j` and `matrix_G K r c i j`
its top `r Ã— r` block and its bottom `c Ã— r` block. The basic open subscheme `V i j` of
`U i` is the locus where `det (matrix_F K r c i j)` does not vanish, so
`(matrix_G K r c i j) * (matrix_F K r c i j)â»Â¹` makes sense as a `c Ã— r` matrix of functions on
`V i j` (we actually use the notations `matrix_F'` and `matrix_G'` for the restrictions of the
matrices to `V i j`). As `U j = Spec (MvPolynomial (Fin c Ã— Fin r) K)`, this defines a morphism
from `V i j` to `U j`, and we check that this morphism factors through the open subscheme
`V j i`, given the desired morphism `t i j : V i j âŸ¶ V j i`.

If `i`, `j` and `k` are in `Basis (Fin (r + c)) K V`, we need to define a gluing morphism
`t' i j k` from `V i j Ã— V i k` (fiber product over `U i`) to `V j k Ã— V j i` (fiber product over
`U j`). The condition `t_fac i j k` says that this morphism should be obtained by "restriction"
(via the first projection on `V i j Ã— V i k` and the second projection on `V j k Ã— V j i`) of
`t i j : V i j âŸ¶ V j i`, so we just need to prove that `t i j` does restrict to a morphism
between the fiber products.

Finally, the cocycle condition on the gluing morphisms `t' i j k` is expressed by the
field `cocycle i j k`.
-/
def glueData : GlueData where
  J := Basis (Fin (r + c)) K V
  U _ := Spec (CommRingCat.of (MvPolynomial (Fin c Ã— Fin r) K))
  V ij := Spec (CommRingCat.of (Localization.Away (equation r c ij.1 ij.2)))
  f i j := open_immersion r c i j
  f_mono _ _ := inferInstance
  f_hasPullback := inferInstance
  f_id i := by
    simp only [open_immersion]
    suffices h : IsIso ((CommRingCat.ofHom (algebraMap (MvPolynomial
      (Fin c Ã— Fin r) K) (Localization.Away (equation r c i i))))) by
      exact inferInstance
    simp only [equation]
    rw [matrix_F_eq_id_of_diagonal r c i, Matrix.det_one]
    exact localization_unit_isIso (CommRingCat.of
      (MvPolynomial ((Fin c) Ã— Fin r) K))
  t i j := transition_Spec r c i j
  t_id i := by
    simp only [transition_Spec, transition, AlgHom.coe_ringHom_mk, RingHom.mapMatrix_apply, id_eq]
    rw [â† Spec.map_id]
    apply congrArg
    change _ = CommRingCat.ofHom (RingHom.id _)
    apply congrArg
    apply IsLocalization.ringHom_ext (R := MvPolynomial (Fin c Ã— Fin r) K)
      (M := Submonoid.powers (equation r c i i))
    ext x
    Â· simp only [AlgHom.coe_ringHom_mk, RingHom.mapMatrix_apply,
      IsLocalization.Away.AwayMap.lift_comp, RingHom.coe_comp, MvPolynomial.coe_evalâ‚‚Hom,
      Function.comp_apply, MvPolynomial.evalâ‚‚_C, RingHomCompTriple.comp_eq]
      rw [â† MvPolynomial.algebraMap_eq]
      rw [IsScalarTower.algebraMap_apply K (MvPolynomial (Fin c Ã— Fin r) K)]
    Â· simp only [AlgHom.coe_ringHom_mk, RingHom.mapMatrix_apply,
      IsLocalization.Away.AwayMap.lift_comp, MvPolynomial.evalâ‚‚Hom_X', RingHomCompTriple.comp_eq]
      simp_rw [matrix_F_eq_id_of_diagonal]
      simp_rw [matrix_G'_eq_X_of_diagonal]
      simp only [map_zero, _root_.map_one, Matrix.map_one, inv_one, Matrix.mul_one, of_apply,
        Prod.mk.eta]
  t' i j k := by
    refine Limits.pullback.lift (transition'â‚_Spec r c i j k)
      (Limits.pullback.fst _ _ â‰« transition_Spec r c i j) ?_
    exact immersion_transition'â‚_Spec _ _ _ _ _
  t_fac i j k := by rw [Limits.pullback.lift_snd]
  cocycle i j k := by
    simp only
    rw [â† cancel_mono (Limits.pullback.snd _ _)]
    conv_lhs => rw [Category.assoc, Category.assoc]
                congr; rfl; congr; rfl
                rw [Limits.pullback.lift_snd]
    slice_lhs 2 3 => rw [Limits.pullback.lift_fst]
    rw [â† cancel_mono (f := open_immersion r c i k), Category.assoc, Category.assoc]
    rw [transition'â‚_transition, Category.id_comp, â† Category.assoc, Limits.pullback.lift_snd]
    slice_lhs 2 3 => rw [transition_transition_flip_Spec]
    rw [Category.id_comp, Limits.pullback.condition]
  f_open _ _ := inferInstance

end Grassmannian

/-- The Grassmannian scheme of locally free of rank `r` quotients of a finite free `R`-module `V`.
(Here `c` is supposed to play the role of `FiniteDimensional.finrank K V - r`; we avoid using
substraction on natural numbers for technical reasons. Note that the Grassmannian as defined here
is empty unless `V` is free and `r + c = FinitDimensional.finrank K V`.)
-/
def Grassmannian := (Grassmannian.glueData K V r c).glued

namespace Grassmannian

/-- The structure morphism from the Grassmannian of locally free of rank `r` quotients of a finite
free `R`-module `V` to `Spec R`.
-/
def structMorphism : Grassmannian K V r c âŸ¶ Spec (CommRingCat.of K) := by
  refine Scheme.GlueData.glueMorphisms (Grassmannian.glueData K V r c)
    (fun _ â†¦ Spec.map (CommRingCat.ofHom (algebraMap _ _))) ?_
  intro i j
  simp only [glueData, open_immersion, transition_Spec]
  rw [â† Spec.map_comp, â† Spec.map_comp, â† Spec.map_comp]
  congr 1
  conv_lhs =>  change (algebraMap _ (Localization.Away (equation r c i j))).comp
                 (algebraMap K (MvPolynomial (Fin c Ã— Fin r) K))
               rw [â† IsScalarTower.algebraMap_eq K]
  conv_rhs => congr; change (algebraMap _ (Localization.Away (equation r c j i))).comp
                 (algebraMap K (MvPolynomial (Fin c Ã— Fin r) K))
              rw [â† IsScalarTower.algebraMap_eq K]
  conv_rhs => change (â†‘(transition r c i j) : Localization.Away (equation r c j i) â†’+*
                Localization.Away (equation r c i j)).comp (algebraMap K _)
              rw [(transition r c i j).comp_algebraMap]

end Grassmannian
