/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
module

public import Mathlib.AlgebraicGeometry.Cover.Directed
public import Mathlib.AlgebraicGeometry.Morphisms.UniversallyInjective

/-!
# Relative gluing

In this file we show a relative gluing lemma (see https://stacks.math.columbia.edu/tag/01LH):
If `{Uáµ¢}` is a locally directed open cover of `S` and we have a compatible family of `Xáµ¢` over `Uáµ¢`,
the `Xáµ¢` glue to a morphism `f : X âŸ¶ S` such that `Xáµ¢ â‰… fâ»Â¹ Uáµ¢`.
-/

@[expose] public section

universe u

open CategoryTheory Limits

namespace AlgebraicGeometry

lemma Scheme.isLocallyDirected_of_equifibered_of_injective {J : Type*} [Category J]
    {F G : J â¥¤ Scheme.{u}} (s : F âŸ¶ G) [Quiver.IsThin J] (hs : s.Equifibered)
    (H : âˆ€ {i j} (hij : i âŸ¶ j), Function.Injective (F.map hij))
    [(G â‹™ Scheme.forget).IsLocallyDirected] :
    (F â‹™ Scheme.forget).IsLocallyDirected where
  cond {i j k} fi fj xi xj heq := by
    simp only [Functor.comp_obj, Scheme.forget_obj, Functor.comp_map, Scheme.forget_map] at heq
    obtain âŸ¨l, fli, flj, x, hi, hjâŸ© := (G â‹™ Scheme.forget).exists_map_eq_of_isLocallyDirected fi fj
        (s.app i xi) (s.app j xj) <| by
      simp only [Functor.comp_obj, Scheme.forget_obj, Functor.comp_map, Scheme.forget_map]
      rw [â† Scheme.Hom.comp_apply, â† s.naturality, Scheme.Hom.comp_apply, heq,
        â† Scheme.Hom.comp_apply, s.naturality]
      simp
    use l, fli, flj
    let e := (hs fli).isoPullback
    obtain âŸ¨z, h1, h2âŸ© := Scheme.Pullback.exists_preimage_pullback xi x hi.symm
    refine âŸ¨e.inv z, ?_, ?_âŸ©
    Â· simp [â† h1, â† Scheme.Hom.comp_apply, e]
    Â· apply H fj
      simp only [Functor.comp_map, Scheme.forget_map, â† Scheme.Hom.comp_apply,
        Category.assoc, â† Functor.map_comp, show flj â‰« fj = fli â‰« fi by subsingleton]
      simp [e, Functor.map_comp, â† heq, h1]

namespace Scheme.Cover

variable {S : Scheme.{u}} (ğ’° : S.OpenCover) [Category ğ’°.Iâ‚€] [ğ’°.LocallyDirected]

/--
A relative gluing datum over a locally directed cover `ğ’°` of `S` is a scheme `Xáµ¢` for every
`i : ğ’°.Iâ‚€` and natural maps `Xáµ¢ âŸ¶ Uáµ¢` such that for every `i âŸ¶ j`, the diagram
```
Xáµ¢ --> Uáµ¢
|      |
v      v
Xâ±¼ --> Uâ±¼
```
is a pullback square. We bundle this in the form of a functor and an equifibered natural
transformation.
The `Xáµ¢` then glue to a scheme over `S`
(see `AlgebraicGeometry.Scheme.Cover.RelativeGluingData.glued`).
-/
@[stacks 01LH]
structure RelativeGluingData where
  /-- The schemes `Xáµ¢`. -/
  functor : ğ’°.Iâ‚€ â¥¤ Scheme.{u}
  /-- The natural maps `Xáµ¢ âŸ¶ Uáµ¢`. -/
  natTrans : functor âŸ¶ ğ’°.functorOfLocallyDirected
  equifibered : natTrans.Equifibered

variable {ğ’°} (d : RelativeGluingData ğ’°)

namespace RelativeGluingData

instance {i j : ğ’°.Iâ‚€} (hij : i âŸ¶ j) : IsOpenImmersion (d.functor.map hij) := by
  apply MorphismProperty.of_isPullback (d.equifibered hij).flip
  infer_instance

instance [Quiver.IsThin ğ’°.Iâ‚€] : (d.functor â‹™ Scheme.forget).IsLocallyDirected := by
  apply isLocallyDirected_of_equifibered_of_injective d.natTrans d.equifibered
  intro i j hij
  exact (d.functor.map hij).injective

variable [Small.{u} ğ’°.Iâ‚€] [Quiver.IsThin ğ’°.Iâ‚€]

/--
The glued scheme of a relative gluing datum is the colimit over the `Xáµ¢`. For the
structure map, see `AlgebraicGeometry.Scheme.Cover.RelativeGluingData.toBase` and the isomorphisms
with the preimages `AlgebraicGeometry.Scheme.Cover.RelativeGluingData.isPullback_natTrans_Î¹_toBase`.
-/
@[stacks 01LH]
noncomputable abbrev glued : Scheme.{u} :=
  colimit d.functor

/-- The cover of the glued `Xáµ¢` given by the `Xáµ¢`. -/
@[simps!]
noncomputable def cover : OpenCover d.glued :=
  Scheme.IsLocallyDirected.openCover _

/-- The structure map from the colimit of the `Xáµ¢` to `S`. -/
noncomputable def toBase : d.glued âŸ¶ S :=
  colimit.desc _
    { pt := S
      Î¹ := d.natTrans â‰« ğ’°.functorOfLocallyDirectedHomBase }

@[reassoc (attr := simp)]
lemma Î¹_toBase (i : ğ’°.Iâ‚€) :
    colimit.Î¹ d.functor i â‰« d.toBase = d.natTrans.app i â‰« ğ’°.f i := by
  simp [toBase]

lemma preimage_toBase_eq_range_Î¹ (i : ğ’°.Iâ‚€) :
    d.toBase â»Â¹' (Set.range <| ğ’°.f i) = Set.range (colimit.Î¹ d.functor i) := by
  ext x
  refine âŸ¨fun âŸ¨ui, hâŸ© â†¦ ?_, ?_âŸ©
  Â· obtain âŸ¨j, xj, rflâŸ© := IsLocallyDirected.Î¹_jointly_surjective _ x
    obtain âŸ¨k, fi, fj, uk, rfl, hâŸ© := ğ’°.exists_of_f_eq_f ui (d.natTrans.app j xj) <| by
      simp only [h, functorOfLocallyDirected_obj, â† Scheme.Hom.comp_apply, Î¹_toBase]
    obtain âŸ¨xk, rfl, h2âŸ© := exists_preimage_of_isPullback (d.equifibered fj) xj uk <| by
      apply (ğ’°.f j).injective
      simp only [functorOfLocallyDirected_obj, functorOfLocallyDirected_map]
      rw [â† Scheme.Hom.comp_apply, â† Î¹_toBase, Scheme.Hom.comp_apply, â† h]
      simp [â† Scheme.Hom.comp_apply]
    use d.functor.map fi xk
    simp [â† Scheme.Hom.comp_apply, â† Scheme.Hom.comp_apply]
  Â· rintro âŸ¨y, rflâŸ©
    use d.natTrans.app i y
    rw [â† Scheme.Hom.comp_apply, Î¹_toBase]
    simp

lemma isPullback_natTrans_Î¹_toBase (i : ğ’°.Iâ‚€) :
    IsPullback (d.natTrans.app i) (colimit.Î¹ d.functor i) (ğ’°.f i) d.toBase := by
  refine âŸ¨by simp, âŸ¨PullbackCone.IsLimit.mk _ ?_ ?_ ?_ ?_âŸ©âŸ©
  Â· intro s
    apply IsOpenImmersion.lift (colimit.Î¹ d.functor i) s.snd
    rw [â† preimage_toBase_eq_range_Î¹]
    rintro x âŸ¨x, rflâŸ©
    use s.fst x
    rw [â† Scheme.Hom.comp_apply, â† s.condition]
    simp
  Â· intro s
    rw [â† cancel_mono (ğ’°.f i), Category.assoc, â† Î¹_toBase, IsOpenImmersion.lift_fac_assoc,
      s.condition]
  Â· simp
  Â· intro s m h1 h2
    simpa [â† cancel_mono (colimit.Î¹ d.functor i)]

end Scheme.Cover.RelativeGluingData

end AlgebraicGeometry
