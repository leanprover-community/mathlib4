/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.AlgebraicGeometry.Morphisms.Affine

/-!
# Algebraic torus

## Main definitions

- `AlgebraicGeometry.Torus`: `ğ•‹(n; S)` is the split algebraic torus of dimension `n` over `S`.
- `AlgebraicGeometry.Torus.coord`: The standard coordinate functions on the torus.
- `AlgebraicGeometry.Torus.homOfVector`:
  The morphism `X âŸ¶ ğ•‹(n; S)` given by a `X âŸ¶ S` and a choice of `n`-coordinate functions.
- `AlgebraicGeometry.Torus.homOverEquiv`:
  `S`-morphisms into `Spec ğ•‹(n; S)` are equivalent to the choice of `n` invertible global sections.
- `AlgebraicGeometry.Torus.SpecIso`: `ğ•‹(n; Spec R) â‰… Spec R[XÌ², XÌ²â»Â¹]`.

-/

open CategoryTheory Limits MvPolynomial

noncomputable section

namespace AlgebraicGeometry

universe v u

variable (n : Type v) (S : Scheme.{max u v})

section Affine

variable (R : Type*) (Ïƒ : Type*) [CommRing R] {S : Type*} [CommRing S]

/-- `R[Ïƒ, Ïƒâ»Â¹]`. This is the ring representing `S â†¦ Fun(Ïƒ, SË£)`. -/
abbrev TorusRing : Type _ :=
  Localization (M := MvPolynomial Ïƒ R) (.closure (Set.range X))

variable {R Ïƒ}

/-- The monomial `Xáµ¢` for `i : Ïƒ` in `R[Ïƒ, Ïƒâ»Â¹]`. -/
abbrev TorusRing.X (i : Ïƒ) : TorusRing R Ïƒ := (algebraMap (MvPolynomial Ïƒ R) (TorusRing R Ïƒ) (.X i))

lemma TorusRing.isUnit_X (i : Ïƒ) : IsUnit (TorusRing.X (R := R) i) :=
  IsLocalization.map_units (R := MvPolynomial Ïƒ R) (TorusRing R Ïƒ)
    (M := .closure (Set.range MvPolynomial.X))
    âŸ¨MvPolynomial.X i, Submonoid.subset_closure âŸ¨i, rflâŸ©âŸ©

/-- The monomial `Xáµ¢` for `i : Ïƒ` as a unit in `R[Ïƒ, Ïƒâ»Â¹]`. -/
abbrev TorusRing.XUnit (i : Ïƒ) : (TorusRing R Ïƒ)Ë£ := (isUnit_X i).unit

/-- To give a ring hom `R[Ïƒ, Ïƒâ»Â¹] â†’+* S`, it suffices to give a ring hom `R â†’+* S` and
an assignment of variables `Ïƒ â†’ SË£`. -/
def TorusRing.lift (f : R â†’+* S) (v : Ïƒ â†’ SË£) : TorusRing R Ïƒ â†’+* S :=
  IsLocalization.lift
    (R := MvPolynomial Ïƒ R) (M := .closure (Set.range MvPolynomial.X))
    (g := MvPolynomial.evalâ‚‚Hom f (Units.val âˆ˜ v)) <| by
    simp only [Subtype.forall]
    show _ â‰¤ (IsUnit.submonoid _).comap _
    simp [Set.range_subset_iff, IsUnit.mem_submonoid_iff]

@[simp]
lemma TorusRing.lift_X (f : R â†’+* S) (v : Ïƒ â†’ SË£) (i : Ïƒ) : TorusRing.lift f v (.X i) = v i := by
  simp [lift]

@[simp]
lemma TorusRing.lift_algebraMap (f : R â†’+* S) (v : Ïƒ â†’ SË£) (r : R) :
    (TorusRing.lift f v (algebraMap R _ r)) = f r := by
  simp [lift, IsScalarTower.algebraMap_apply R (MvPolynomial Ïƒ R)]

lemma TorusRing.lift_comp (f : R â†’+* S) (v : Ïƒ â†’ SË£) :
    (TorusRing.lift f v).comp (algebraMap R _) = f := by
  ext; exact TorusRing.lift_algebraMap f v _

@[ext high]
lemma TorusRing.ringHom_ext (f g : TorusRing R Ïƒ â†’+* S)
    (hâ‚ : f.comp (algebraMap R _) = g.comp (algebraMap R _))
    (hâ‚‚ : âˆ€ i, f (.X i) = g (.X i)) : f = g := by
  apply IsLocalization.ringHom_ext
      (R := MvPolynomial Ïƒ R) (M := .closure (Set.range MvPolynomial.X))
  ext x
  Â· exact DFunLike.congr_fun hâ‚ x
  Â· exact hâ‚‚ _

/-- The ring equiv `R[Ïƒ, Ïƒâ»Â¹] â‰… S[Ïƒ, Ïƒâ»Â¹]` if `R â‰… S`. -/
def TorusRing.equiv (e : R â‰ƒ+* S) : TorusRing R Ïƒ â‰ƒ+* TorusRing S Ïƒ where
  __ := TorusRing.lift ((algebraMap S (TorusRing S Ïƒ)).comp e.toRingHom) XUnit
  invFun := TorusRing.lift ((algebraMap R (TorusRing R Ïƒ)).comp e.symm.toRingHom) XUnit
  left_inv x := by
    dsimp
    show _ = RingHom.id _ x
    rw [â† RingHom.comp_apply]
    congr 1
    ext <;> simp
  right_inv x := by
    dsimp
    show _ = RingHom.id _ x
    rw [â† RingHom.comp_apply]
    congr 1
    ext <;> simp

end Affine

local notation3 "ğ•‹[" n "]" => CommRingCat.of (TorusRing (ULift â„¤) n)
local notation3 "ğ•‹[" n "].{" u "," v "}" => CommRingCat.of (TorusRing (ULift.{max u v} â„¤) n)

/-- `ğ•‹(n; S)` is the split algebraic torus of dimension `n` over `S`.
Note that `n` is an arbitrary index type (e.g. `Fin m`). -/
def Torus (n : Type v) (S : Scheme.{max u v}) : Scheme.{max u v} :=
  pullback (terminal.from S) (terminal.from (Spec ğ•‹[n].{u, v}))

namespace Torus

/-- `ğ•‹(n; S)` is the (split) algebraic torus of dimension `n`-space over `S`. -/
scoped [AlgebraicGeometry] notation "ğ•‹("n"; "S")" => Torus n S

variable {n} in
lemma torusRing_int_ringHom_ext {R} {f g : ğ•‹[n].{u, v} âŸ¶ R} (h : âˆ€ i, f (.X i) = g (.X i)) :
    f = g := by
  ext1
  apply IsLocalization.ringHom_ext
    (R := MvPolynomial n (ULift.{max u v} â„¤)) (M := .closure (Set.range MvPolynomial.X))
  ext x
  Â· obtain âŸ¨x, rflâŸ© := ULift.ringEquiv.symm.surjective x
    simp
  Â· simpa using h x

@[simps (config := .lemmasOnly)]
instance over : ğ•‹(n; S).CanonicallyOver S where
  hom := pullback.fst _ _

/-- The map from the split algebraic torus over `S` to the integral model `Spec â„¤[XÌ², XÌ²â»Â¹]`. -/
def toSpecTorusRing : ğ•‹(n; S) âŸ¶ Spec ğ•‹[n].{u, v} := pullback.snd _ _

variable {X : Scheme.{max u v}}

/--
Morphisms into `Spec ğ•‹[n]` are equivalent the choice of `n` invertible global sections.
Use `homOverEquiv` instead.
-/
@[simps]
def toSpecTorusRingEquiv : (X âŸ¶ Spec ğ•‹[n].{u, v}) â‰ƒ (n â†’ Î“(X, âŠ¤)Ë£) where
  toFun f i := (TorusRing.XUnit i).map
    ((Scheme.Î“SpecIso ğ•‹[n].{u, v}).inv â‰« f.appTop).hom.toMonoidHom
  invFun v := X.toSpecÎ“ â‰« Spec.map (CommRingCat.ofHom
    (TorusRing.lift ((Int.castRingHom _).comp ULift.ringEquiv.toRingHom) v))
  left_inv f := by
    show Î“Spec.adjunction.homEquiv _ _ _ = _
    rw [â† Equiv.eq_symm_apply, Adjunction.homEquiv_symm_apply]
    apply Quiver.Hom.unop_inj
    apply torusRing_int_ringHom_ext
    simp [TorusRing.lift_X]
  right_inv v := by
    ext i
    simp [TorusRing.lift_X]

lemma toSpecTorusRingEquiv_comp {X Y : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Spec ğ•‹[n].{u, v}) (i) :
    toSpecTorusRingEquiv n (f â‰« g) i = f.appTop (toSpecTorusRingEquiv n g i) := rfl

variable {n} in
/-- The standard coordinates in `Î“(ğ•‹(n; S), âŠ¤)Ë£` of the split algebraic torus. -/
def coord (i : n) : Î“(ğ•‹(n; S), âŠ¤)Ë£ := toSpecTorusRingEquiv _ (toSpecTorusRing n S) i

section homOfVector

variable {n S}

/-- The morphism `X âŸ¶ ğ•‹(n; S)` given by a `X âŸ¶ S` and a choice of `n`-coordinate functions. -/
def homOfVector (f : X âŸ¶ S) (v : n â†’ Î“(X, âŠ¤)Ë£) : X âŸ¶ ğ•‹(n; S) :=
  pullback.lift f ((toSpecTorusRingEquiv n).symm v) (terminal.hom_ext _ _)

variable (f : X âŸ¶ S) (v : n â†’ Î“(X, âŠ¤)Ë£)

@[reassoc (attr := simp)]
lemma homOfVector_over : homOfVector f v â‰« ğ•‹(n; S) â†˜ S = f :=
  pullback.lift_fst _ _ _

@[reassoc]
lemma homOfVector_toSpecMvPoly :
    homOfVector f v â‰« toSpecTorusRing n S = (toSpecTorusRingEquiv n).symm v :=
  pullback.lift_snd _ _ _

@[simp]
lemma homOfVector_appTop_coord (i) :
    (homOfVector f v).appTop (coord S i) = v i := by
  rw [coord, â† toSpecTorusRingEquiv_comp, homOfVector_toSpecMvPoly,
    Equiv.apply_symm_apply]

@[ext 1100]
lemma hom_ext {f g : X âŸ¶ ğ•‹(n; S)}
    (hâ‚ : f â‰« ğ•‹(n; S) â†˜ S = g â‰« ğ•‹(n; S) â†˜ S)
    (hâ‚‚ : âˆ€ i, f.appTop (coord S i) = g.appTop (coord S i).val) : f = g := by
  apply pullback.hom_ext hâ‚
  show f â‰« toSpecTorusRing _ _ = g â‰« toSpecTorusRing _ _
  apply (toSpecTorusRingEquiv n).injective
  ext i
  rw [toSpecTorusRingEquiv_comp, toSpecTorusRingEquiv_comp]
  exact hâ‚‚ i

@[reassoc]
lemma comp_homOfVector {X Y : Scheme} (v : n â†’ Î“(Y, âŠ¤)Ë£) (f : X âŸ¶ Y) (g : Y âŸ¶ S) :
    f â‰« homOfVector g v = homOfVector (f â‰« g) (Units.map f.appTop.hom.toMonoidHom âˆ˜ v) := by
  ext1 <;> simp

end homOfVector

variable [X.Over S]

variable {n}

instance (v : n â†’ Î“(X, âŠ¤)Ë£) : (homOfVector (X â†˜ S) v).IsOver S where

/-- `S`-morphisms into `Spec ğ•‹(n; S)` are equivalent to the choice of `n` global sections. -/
@[simps]
def homOverEquiv : { f : X âŸ¶ ğ•‹(n; S) // f.IsOver S } â‰ƒ (n â†’ Î“(X, âŠ¤)Ë£) where
  toFun f i := (coord S i).map f.1.appTop.hom.toMonoidHom
  invFun v := âŸ¨homOfVector (X â†˜ S) v, inferInstanceâŸ©
  left_inv f := by
    ext : 2
    Â· simp [f.2.1]
    Â· rw [homOfVector_appTop_coord]
      rfl
  right_inv v := by ext i; simp [-TopologicalSpace.Opens.map_top, homOfVector_appTop_coord]

lemma isoSpec_inv_appTop (S : Scheme.{u}) [IsAffine S] :
    S.isoSpec.inv.appTop = (Scheme.Î“SpecIso _).inv := by
  rw [â† Iso.comp_hom_eq_id, â† Scheme.toSpecÎ“_appTop, â† Scheme.comp_appTop,
    Scheme.toSpecÎ“_isoSpec_inv, Scheme.id_appTop]

variable (n) in
/--
The (split) torus over `Spec R` indexed by `Ïƒ` is isomorphic to `Spec R[Ïƒ, Ïƒâ»Â¹]`.
Also see `Torus.SpecIso`.
-/
@[simps (config := .lemmasOnly) hom inv]
def isoOfIsAffine [IsAffine S] :
    ğ•‹(n; S) â‰… Spec (.of (TorusRing Î“(S, âŠ¤) n)) where
  hom := ğ•‹(n; S).toSpecÎ“ â‰«
    Spec.map (CommRingCat.ofHom (TorusRing.lift (ğ•‹(n; S) â†˜ S).appTop.hom (coord S)))
  inv := homOfVector (Spec.map (CommRingCat.ofHom (algebraMap Î“(S, âŠ¤) _)) â‰« S.isoSpec.inv)
    fun i â†¦ (TorusRing.XUnit (R := Î“(S, âŠ¤)) i).map
      (Scheme.Î“SpecIso (.of (TorusRing Î“(S, âŠ¤) n))).inv.hom.toMonoidHom
  hom_inv_id := by
    ext1
    Â· simp only [Category.assoc, homOfVector_over, comp_over]
      rw [â† Spec.map_comp_assoc, â† CommRingCat.ofHom_comp, TorusRing.lift_comp,
        CommRingCat.ofHom_hom, â† Scheme.toSpecÎ“_naturality_assoc, â† Scheme.isoSpec_hom,
        Iso.hom_inv_id, Category.comp_id]
    Â· -- simp says
      simp only [RingHom.toMonoidHom_eq_coe, Category.assoc, Scheme.comp_app,
        Scheme.comp_coeBase, TopologicalSpace.Opens.map_comp_obj, TopologicalSpace.Opens.map_top,
        Scheme.toSpecÎ“_appTop, Scheme.Î“SpecIso_naturality, CommRingCat.hom_comp,
        CommRingCat.hom_ofHom, RingHom.coe_comp, Function.comp_apply, homOfVector_appTop_coord,
        Units.coe_map, IsUnit.unit_spec, MonoidHom.coe_coe, Scheme.id_app, CommRingCat.hom_id,
        RingHom.id_apply]
      rw [CommRingCat.hom_inv_apply, TorusRing.lift_X]
  inv_hom_id := by
    apply ext_of_isAffine
    simp only [RingHom.toMonoidHom_eq_coe, Scheme.comp_appTop,
      Scheme.toSpecÎ“_appTop, Scheme.Î“SpecIso_naturality, Category.assoc, Scheme.id_app,
      â† Iso.eq_inv_comp, Category.comp_id]
    ext1
    apply TorusRing.ringHom_ext
    Â· ext
      simp only [CommRingCat.hom_comp, CommRingCat.hom_ofHom, RingHom.coe_comp, Function.comp_apply,
        TorusRing.lift_algebraMap]
      simp only [â† RingHom.comp_apply, â† CommRingCat.hom_comp, â† Scheme.comp_appTop,
        homOfVector_over]
      simp only [Scheme.comp_appTop, isoSpec_inv_appTop, CommRingCat.of_carrier,
        â† Scheme.Î“SpecIso_inv_naturality]
      rfl
    Â· intro i
      simp [TorusRing.lift_X]

@[simp]
lemma isoOfIsAffine_hom_appTop [IsAffine S] :
    (isoOfIsAffine.{v, u} n S).hom.appTop = (Scheme.Î“SpecIso _).hom â‰«
      CommRingCat.ofHom (TorusRing.lift (R := Î“(S, âŠ¤)) (Ïƒ := n)
        (ğ•‹(n; S) â†˜ S).appTop.hom (coord S)) := by
  simp [isoOfIsAffine]

@[simp]
lemma isoOfIsAffine_inv_appTop_coord [IsAffine S] (i) :
    (isoOfIsAffine n S).inv.appTop (coord S i) = (Scheme.Î“SpecIso (.of _)).inv (.X i) :=
  homOfVector_appTop_coord _ _ _

@[reassoc (attr := simp)]
lemma isoOfIsAffine_inv_over [IsAffine S] :
    (isoOfIsAffine n S).inv â‰« ğ•‹(n; S) â†˜ S =
      Spec.map (CommRingCat.ofHom (algebraMap _ _)) â‰« S.isoSpec.inv :=
  pullback.lift_fst _ _ _

instance [IsAffine S] : IsAffine ğ•‹(n; S) := isAffine_of_isIso (isoOfIsAffine n S).hom

variable (n) in
/-- The (split) algebraic torus over `Spec R` indexed by `Ïƒ` is isomorphic to `Spec R[Ïƒ, Ïƒâ»Â¹]`. -/
def SpecIso (R : CommRingCat.{max u v}) :
    ğ•‹(n; Spec R) â‰… Spec (.of (TorusRing R n)) :=
  isoOfIsAffine _ _ â‰ªâ‰« Scheme.Spec.mapIso
    ((TorusRing.equiv (Scheme.Î“SpecIso R).commRingCatIsoToRingEquiv.symm).toCommRingCatIso).op

@[simp]
lemma SpecIso_inv_appTop_coord (R : CommRingCat.{max u v}) (i) :
    (SpecIso n R).inv.appTop (coord (Spec R) i) = (Scheme.Î“SpecIso (.of _)).inv (.X i) := by
  simp only [SpecIso, Iso.trans_inv, Functor.mapIso_inv, Iso.op_inv, RingEquiv.toCommRingCatIso_inv,
    Scheme.Spec_map, Quiver.Hom.unop_op, Scheme.comp_app, TopologicalSpace.Opens.map_top,
    CommRingCat.hom_comp, RingHom.coe_comp, Function.comp_apply, isoOfIsAffine_inv_appTop_coord]
  rw [â† CommRingCat.comp_apply, â† Scheme.Î“SpecIso_inv_naturality,
      CommRingCat.comp_apply]
  congr 1
  exact TorusRing.lift_X _ _ _

@[reassoc (attr := simp)]
lemma SpecIso_inv_over (R : CommRingCat.{max u v}) :
    (SpecIso n R).inv â‰« ğ•‹(n; Spec R) â†˜ Spec R = Spec.map (CommRingCat.ofHom (algebraMap _ _)) := by
  simp only [SpecIso, Iso.trans_inv, Functor.mapIso_inv, Iso.op_inv,
    mapEquiv_symm, RingEquiv.toRingHom_eq_coe, Scheme.Spec_map, Quiver.Hom.unop_op, Category.assoc,
    isoOfIsAffine_inv_over, Scheme.isoSpec_Spec_inv, â† Spec.map_comp]
  congr 1
  rw [Iso.inv_comp_eq]
  ext : 2
  exact TorusRing.lift_algebraMap _ _ _

section functorial

variable (n) in
/-- `ğ”¸(n; S)` is functorial wrt `S`. -/
def map {S T : Scheme.{max u v}} (f : S âŸ¶ T) : ğ•‹(n; S) âŸ¶ ğ•‹(n; T) :=
  homOfVector (ğ•‹(n; S) â†˜ S â‰« f) (coord S)

@[reassoc (attr := simp)]
lemma map_over {S T : Scheme.{max u v}} (f : S âŸ¶ T) : map n f â‰« ğ•‹(n; T) â†˜ T = ğ•‹(n; S) â†˜ S â‰« f :=
  pullback.lift_fst _ _ _

@[simp]
lemma map_appTop_coord {S T : Scheme.{max u v}} (f : S âŸ¶ T) (i) :
    (map n f).appTop (coord T i) = (coord S i).1 :=
  homOfVector_appTop_coord _ _ _

@[reassoc (attr := simp)]
lemma map_toSpecTorusRing {S T : Scheme.{max u v}} (f : S âŸ¶ T) :
    map n f â‰« toSpecTorusRing n T = toSpecTorusRing n S := by
  apply (toSpecTorusRingEquiv _).injective
  ext i
  rw [toSpecTorusRingEquiv_comp, â† coord, map_appTop_coord, coord]

@[simp]
lemma map_id : map n (ğŸ™ S) = ğŸ™ ğ•‹(n; S) := by
  ext1 <;> simp

@[reassoc, simp]
lemma map_comp {S S' S'' : Scheme} (f : S âŸ¶ S') (g : S' âŸ¶ S'') :
    map n (f â‰« g) = map n f â‰« map n g := by
  ext1
  Â· simp
  Â· simp

lemma isPullback_map {S T : Scheme.{max u v}} (f : S âŸ¶ T) :
    IsPullback (map n f) (ğ•‹(n; S) â†˜ S) (ğ•‹(n; T) â†˜ T) f := by
  refine (IsPullback.paste_horiz_iff (.flip <| .of_hasPullback _ _) (map_over f)).mp ?_
  simp only [terminal.comp_from, ]
  convert (IsPullback.of_hasPullback _ _).flip
  rw [â† toSpecTorusRing, â† toSpecTorusRing, map_toSpecTorusRing]

/-- `ğ•‹(n; S)` is functorial wrt `n`. -/
def reindex {n m : Type v} (i : m â†’ n) (S : Scheme.{max u v}) : ğ•‹(n; S) âŸ¶ ğ•‹(m; S) :=
  homOfVector (ğ•‹(n; S) â†˜ S) (coord S âˆ˜ i)

@[simp, reassoc]
lemma reindex_over {n m : Type v} (i : m â†’ n) (S : Scheme.{max u v}) :
    reindex i S â‰« ğ•‹(m; S) â†˜ S = ğ•‹(n; S) â†˜ S :=
  pullback.lift_fst _ _ _

@[simp]
lemma reindex_appTop_coord {n m : Type v} (i : m â†’ n) (S : Scheme.{max u v}) (j : m) :
    (reindex i S).appTop (coord S j) = coord S (i j) :=
  homOfVector_appTop_coord _ _ _

@[simp]
lemma reindex_id : reindex id S = ğŸ™ ğ•‹(n; S) := by
  ext1 <;> simp

@[simp, reassoc]
lemma reindex_comp {nâ‚ nâ‚‚ nâ‚ƒ : Type v} (i : nâ‚ â†’ nâ‚‚) (j : nâ‚‚ â†’ nâ‚ƒ) (S : Scheme.{max u v}) :
    reindex (j âˆ˜ i) S = reindex j S â‰« reindex i S := by
  have Hâ‚ : reindex (j âˆ˜ i) S â‰« ğ•‹(nâ‚; S) â†˜ S = (reindex j S â‰« reindex i S) â‰« ğ•‹(nâ‚; S) â†˜ S := by simp
  have Hâ‚‚ (k) : (reindex (j âˆ˜ i) S).appTop (coord S k) =
      (reindex j S).appTop ((reindex i S).appTop (coord S k).1) := by
    rw [reindex_appTop_coord, reindex_appTop_coord, reindex_appTop_coord]
    rfl
  exact hom_ext Hâ‚ Hâ‚‚

@[reassoc (attr := simp)]
lemma map_reindex {nâ‚ nâ‚‚ : Type v} (i : nâ‚ â†’ nâ‚‚) {S T : Scheme.{max u v}} (f : S âŸ¶ T) :
    map nâ‚‚ f â‰« reindex i T = reindex i S â‰« map nâ‚ f := by
  apply hom_ext <;> simp

/-- The torus as a functor. -/
@[simps]
def functor : (Type v)áµ’áµ– â¥¤ Scheme.{max u v} â¥¤ Scheme.{max u v} where
  obj n := { obj := Torus n.unop, map := map n.unop, map_id := map_id, map_comp := map_comp }
  map {n m} i := { app := reindex i.unop, naturality := fun _ _ â†¦ map_reindex i.unop }
  map_id n := by ext: 2; exact reindex_id _
  map_comp f g := by ext: 2; dsimp; exact reindex_comp _ _ _

end functorial

section instances

instance : IsAffineHom (ğ•‹(n; S) â†˜ S) := MorphismProperty.pullback_fst _ _ inferInstance

end instances

end Torus

end AlgebraicGeometry
