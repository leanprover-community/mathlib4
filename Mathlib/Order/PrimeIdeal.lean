/-
Copyright (c) 2021 Noam Atar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Noam Atar
-/
import Mathlib.Order.Ideal
import Mathlib.Order.PFilter

#align_import order.prime_ideal from "leanprover-community/mathlib"@"740acc0e6f9adf4423f92a485d0456fc271482da"

/-!
# Prime ideals

## Main definitions

Throughout this file, `P` is at least a preorder, but some sections require more
structure, such as a bottom element, a top element, or a join-semilattice structure.

- `Order.Ideal.PrimePair`: A pair of an `Order.Ideal` and an `Order.PFilter` which form a partition
  of `P`.  This is useful as giving the data of a prime ideal is the same as giving the data of a
  prime filter.
- `Order.Ideal.IsPrime`: a predicate for prime ideals. Dual to the notion of a prime filter.
- `Order.PFilter.IsPrime`: a predicate for prime filters. Dual to the notion of a prime ideal.

## References

- <https://en.wikipedia.org/wiki/Ideal_(order_theory)>

## Tags

ideal, prime

-/


open Order.PFilter

namespace Order

variable {P : Type*}

namespace Ideal

/-- A pair of an `Order.Ideal` and an `Order.PFilter` which form a partition of `P`.
-/
-- porting note: no attr @[nolint has_nonempty_instance]
structure PrimePair (P : Type*) [Preorder P] where
  I : Ideal P
  F : PFilter P
  isCompl_I_F : IsCompl (I : Set P) F
#align order.ideal.prime_pair Order.Ideal.PrimePair

namespace PrimePair

variable [Preorder P] (IF : PrimePair P)

theorem compl_I_eq_F : (IF.I : Set P)·∂ú = IF.F :=
  IF.isCompl_I_F.compl_eq
set_option linter.uppercaseLean3 false in
#align order.ideal.prime_pair.compl_I_eq_F Order.Ideal.PrimePair.compl_I_eq_F

theorem compl_F_eq_I : (IF.F : Set P)·∂ú = IF.I :=
  IF.isCompl_I_F.eq_compl.symm
set_option linter.uppercaseLean3 false in
#align order.ideal.prime_pair.compl_F_eq_I Order.Ideal.PrimePair.compl_F_eq_I

theorem I_isProper : IsProper IF.I := by
  cases' IF.F.nonempty with w h
  -- ‚ä¢ IsProper IF.I
  apply isProper_of_not_mem (_ : w ‚àâ IF.I)
  -- ‚ä¢ ¬¨w ‚àà IF.I
  rwa [‚Üê IF.compl_I_eq_F] at h
  -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align order.ideal.prime_pair.I_is_proper Order.Ideal.PrimePair.I_isProper

protected theorem disjoint : Disjoint (IF.I : Set P) IF.F :=
  IF.isCompl_I_F.disjoint
#align order.ideal.prime_pair.disjoint Order.Ideal.PrimePair.disjoint

theorem I_union_F : (IF.I : Set P) ‚à™ IF.F = Set.univ :=
  IF.isCompl_I_F.sup_eq_top
set_option linter.uppercaseLean3 false in
#align order.ideal.prime_pair.I_union_F Order.Ideal.PrimePair.I_union_F

theorem F_union_I : (IF.F : Set P) ‚à™ IF.I = Set.univ :=
  IF.isCompl_I_F.symm.sup_eq_top
set_option linter.uppercaseLean3 false in
#align order.ideal.prime_pair.F_union_I Order.Ideal.PrimePair.F_union_I

end PrimePair

/-- An ideal `I` is prime if its complement is a filter.
-/
@[mk_iff]
class IsPrime [Preorder P] (I : Ideal P) extends IsProper I : Prop where
  compl_filter : IsPFilter (I : Set P)·∂ú
#align order.ideal.is_prime Order.Ideal.IsPrime

section Preorder

variable [Preorder P]

/-- Create an element of type `Order.Ideal.PrimePair` from an ideal satisfying the predicate
`Order.Ideal.IsPrime`. -/
def IsPrime.toPrimePair {I : Ideal P} (h : IsPrime I) : PrimePair P :=
  { I
    F := h.compl_filter.toPFilter
    isCompl_I_F := isCompl_compl }
#align order.ideal.is_prime.to_prime_pair Order.Ideal.IsPrime.toPrimePair

theorem PrimePair.I_isPrime (IF : PrimePair P) : IsPrime IF.I :=
  { IF.I_isProper with
    compl_filter := by
      rw [IF.compl_I_eq_F]
      -- ‚ä¢ IsPFilter ‚ÜëIF.F
      exact IF.F.isPFilter }
      -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align order.ideal.prime_pair.I_is_prime Order.Ideal.PrimePair.I_isPrime

end Preorder

section SemilatticeInf

variable [SemilatticeInf P] {x y : P} {I : Ideal P}

theorem IsPrime.mem_or_mem (hI : IsPrime I) {x y : P} : x ‚äì y ‚àà I ‚Üí x ‚àà I ‚à® y ‚àà I := by
  contrapose!
  -- ‚ä¢ ¬¨x ‚àà I ‚àß ¬¨y ‚àà I ‚Üí ¬¨x ‚äì y ‚àà I
  let F := hI.compl_filter.toPFilter
  -- ‚ä¢ ¬¨x ‚àà I ‚àß ¬¨y ‚àà I ‚Üí ¬¨x ‚äì y ‚àà I
  show x ‚àà F ‚àß y ‚àà F ‚Üí x ‚äì y ‚àà F
  -- ‚ä¢ x ‚àà F ‚àß y ‚àà F ‚Üí x ‚äì y ‚àà F
  exact fun h => inf_mem h.1 h.2
  -- üéâ no goals
#align order.ideal.is_prime.mem_or_mem Order.Ideal.IsPrime.mem_or_mem

theorem IsPrime.of_mem_or_mem [IsProper I] (hI : ‚àÄ {x y : P}, x ‚äì y ‚àà I ‚Üí x ‚àà I ‚à® y ‚àà I) :
    IsPrime I := by
  rw [IsPrime_iff]
  -- ‚ä¢ IsProper I ‚àß IsPFilter (‚ÜëI)·∂ú
  use ‚Äπ_‚Ä∫
  -- ‚ä¢ IsPFilter (‚ÜëI)·∂ú
  refine .of_def ?_ ?_ ?_
  ¬∑ exact Set.nonempty_compl.2 (I.IsProper_iff.1 ‚Äπ_‚Ä∫)
    -- üéâ no goals
  ¬∑ intro x hx y hy
    -- ‚ä¢ ‚àÉ z, z ‚àà (‚ÜëI)·∂ú ‚àß (fun x x_1 => x ‚â• x_1) x z ‚àß (fun x x_1 => x ‚â• x_1) y z
    exact ‚ü®x ‚äì y, fun h => (hI h).elim hx hy, inf_le_left, inf_le_right‚ü©
    -- üéâ no goals
  ¬∑ exact @mem_compl_of_ge _ _ _
    -- üéâ no goals
#align order.ideal.is_prime.of_mem_or_mem Order.Ideal.IsPrime.of_mem_or_mem

theorem isPrime_iff_mem_or_mem [IsProper I] : IsPrime I ‚Üî ‚àÄ {x y : P}, x ‚äì y ‚àà I ‚Üí x ‚àà I ‚à® y ‚àà I :=
  ‚ü®IsPrime.mem_or_mem, IsPrime.of_mem_or_mem‚ü©
#align order.ideal.is_prime_iff_mem_or_mem Order.Ideal.isPrime_iff_mem_or_mem

end SemilatticeInf

section DistribLattice

variable [DistribLattice P] {I : Ideal P}

instance (priority := 100) IsMaximal.isPrime [IsMaximal I] : IsPrime I := by
  rw [isPrime_iff_mem_or_mem]
  -- ‚ä¢ ‚àÄ {x y : P}, x ‚äì y ‚àà I ‚Üí x ‚àà I ‚à® y ‚àà I
  intro x y
  -- ‚ä¢ x ‚äì y ‚àà I ‚Üí x ‚àà I ‚à® y ‚àà I
  contrapose!
  -- ‚ä¢ ¬¨x ‚àà I ‚àß ¬¨y ‚àà I ‚Üí ¬¨x ‚äì y ‚àà I
  rintro ‚ü®hx, hynI‚ü© hxy
  -- ‚ä¢ False
  apply hynI
  -- ‚ä¢ y ‚àà I
  let J := I ‚äî principal x
  -- ‚ä¢ y ‚àà I
  have hJuniv : (J : Set P) = Set.univ :=
    IsMaximal.maximal_proper (lt_sup_principal_of_not_mem ‚Äπ_‚Ä∫)
  have hyJ : y ‚àà ‚ÜëJ := Set.eq_univ_iff_forall.mp hJuniv y
  -- ‚ä¢ y ‚àà I
  rw [coe_sup_eq] at hyJ
  -- ‚ä¢ y ‚àà I
  rcases hyJ with ‚ü®a, ha, b, hb, hy‚ü©
  -- ‚ä¢ y ‚àà I
  rw [hy]
  -- ‚ä¢ a ‚äî b ‚àà I
  refine' sup_mem ha (I.lower (le_inf hb _) hxy)
  -- ‚ä¢ b ‚â§ y
  rw [hy]
  -- ‚ä¢ b ‚â§ a ‚äî b
  exact le_sup_right
  -- üéâ no goals
#align order.ideal.is_maximal.is_prime Order.Ideal.IsMaximal.isPrime

end DistribLattice

section BooleanAlgebra

variable [BooleanAlgebra P] {x : P} {I : Ideal P}

theorem IsPrime.mem_or_compl_mem (hI : IsPrime I) : x ‚àà I ‚à® x·∂ú ‚àà I := by
  apply hI.mem_or_mem
  -- ‚ä¢ x ‚äì x·∂ú ‚àà I
  rw [inf_compl_eq_bot]
  -- ‚ä¢ ‚ä• ‚àà I
  exact I.bot_mem
  -- üéâ no goals
#align order.ideal.is_prime.mem_or_compl_mem Order.Ideal.IsPrime.mem_or_compl_mem

theorem IsPrime.mem_compl_of_not_mem (hI : IsPrime I) (hxnI : x ‚àâ I) : x·∂ú ‚àà I :=
  hI.mem_or_compl_mem.resolve_left hxnI
#align order.ideal.is_prime.mem_compl_of_not_mem Order.Ideal.IsPrime.mem_compl_of_not_mem

theorem isPrime_of_mem_or_compl_mem [IsProper I] (h : ‚àÄ {x : P}, x ‚àà I ‚à® x·∂ú ‚àà I) : IsPrime I := by
  simp only [isPrime_iff_mem_or_mem, or_iff_not_imp_left]
  -- ‚ä¢ ‚àÄ {x y : P}, x ‚äì y ‚àà I ‚Üí ¬¨x ‚àà I ‚Üí y ‚àà I
  intro x y hxy hxI
  -- ‚ä¢ y ‚àà I
  have hxcI : x·∂ú ‚àà I := h.resolve_left hxI
  -- ‚ä¢ y ‚àà I
  have ass : x ‚äì y ‚äî y ‚äì x·∂ú ‚àà I := sup_mem hxy (I.lower inf_le_right hxcI)
  -- ‚ä¢ y ‚àà I
  rwa [inf_comm, sup_inf_inf_compl] at ass
  -- üéâ no goals
#align order.ideal.is_prime_of_mem_or_compl_mem Order.Ideal.isPrime_of_mem_or_compl_mem

theorem isPrime_iff_mem_or_compl_mem [IsProper I] : IsPrime I ‚Üî ‚àÄ {x : P}, x ‚àà I ‚à® x·∂ú ‚àà I :=
  ‚ü®fun h _ => h.mem_or_compl_mem, isPrime_of_mem_or_compl_mem‚ü©
#align order.ideal.is_prime_iff_mem_or_compl_mem Order.Ideal.isPrime_iff_mem_or_compl_mem

instance (priority := 100) IsPrime.isMaximal [IsPrime I] : IsMaximal I := by
  simp only [IsMaximal_iff, Set.eq_univ_iff_forall, IsPrime.toIsProper, true_and]
  -- ‚ä¢ ‚àÄ ‚¶ÉJ : Ideal P‚¶Ñ, I < J ‚Üí ‚àÄ (x : P), x ‚àà ‚ÜëJ
  intro J hIJ x
  -- ‚ä¢ x ‚àà ‚ÜëJ
  rcases Set.exists_of_ssubset hIJ with ‚ü®y, hyJ, hyI‚ü©
  -- ‚ä¢ x ‚àà ‚ÜëJ
  suffices ass : x ‚äì y ‚äî x ‚äì y·∂ú ‚àà J
  -- ‚ä¢ x ‚àà ‚ÜëJ
  ¬∑ rwa [sup_inf_inf_compl] at ass
    -- üéâ no goals
  exact
    sup_mem (J.lower inf_le_right hyJ)
      (hIJ.le <| I.lower inf_le_right <| IsPrime.mem_compl_of_not_mem ‚Äπ_‚Ä∫ hyI)
#align order.ideal.is_prime.is_maximal Order.Ideal.IsPrime.isMaximal

end BooleanAlgebra

end Ideal

namespace PFilter

variable [Preorder P]

/-- A filter `F` is prime if its complement is an ideal.
-/
@[mk_iff]
class IsPrime (F : PFilter P) : Prop where
  compl_ideal : IsIdeal (F : Set P)·∂ú
#align order.pfilter.is_prime Order.PFilter.IsPrime

/-- Create an element of type `Order.Ideal.PrimePair` from a filter satisfying the predicate
`Order.PFilter.IsPrime`. -/
def IsPrime.toPrimePair {F : PFilter P} (h : IsPrime F) : Ideal.PrimePair P :=
  { I := h.compl_ideal.toIdeal
    F
    isCompl_I_F := isCompl_compl.symm }
#align order.pfilter.is_prime.to_prime_pair Order.PFilter.IsPrime.toPrimePair

theorem _root_.Order.Ideal.PrimePair.F_isPrime (IF : Ideal.PrimePair P) : IsPrime IF.F :=
  {
    compl_ideal := by
      rw [IF.compl_F_eq_I]
      -- ‚ä¢ IsIdeal ‚ÜëIF.I
      exact IF.I.isIdeal }
      -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align order.ideal.prime_pair.F_is_prime Order.Ideal.PrimePair.F_isPrime

end PFilter

end Order
