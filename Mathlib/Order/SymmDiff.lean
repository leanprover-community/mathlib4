/-
Copyright (c) 2021 Bryan Gin-ge Chen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz, Bryan Gin-ge Chen, YaÃ«l Dillies
-/
import Mathlib.Order.BooleanAlgebra
import Mathlib.Logic.Equiv.Basic

#align_import order.symm_diff from "leanprover-community/mathlib"@"6eb334bd8f3433d5b08ba156b8ec3e6af47e1904"

/-!
# Symmetric difference and bi-implication

This file defines the symmetric difference and bi-implication operators in (co-)Heyting algebras.

## Examples

Some examples are
* The symmetric difference of two sets is the set of elements that are in either but not both.
* The symmetric difference on propositions is `Xor'`.
* The symmetric difference on `Bool` is `Bool.xor`.
* The equivalence of propositions. Two propositions are equivalent if they imply each other.
* The symmetric difference translates to addition when considering a Boolean algebra as a Boolean
  ring.

## Main declarations

* `symmDiff`: The symmetric difference operator, defined as `(a \ b) âŠ” (b \ a)`
* `bihimp`: The bi-implication operator, defined as `(b â‡¨ a) âŠ“ (a â‡¨ b)`

In generalized Boolean algebras, the symmetric difference operator is:

* `symmDiff_comm`: commutative, and
* `symmDiff_assoc`: associative.

## Notations

* `a âˆ† b`: `symmDiff a b`
* `a â‡” b`: `bihimp a b`

## References

The proof of associativity follows the note "Associativity of the Symmetric Difference of Sets: A
Proof from the Book" by John McCuan:

* <https://people.math.gatech.edu/~mccuan/courses/4317/symmetricdifference.pdf>

## Tags

boolean ring, generalized boolean algebra, boolean algebra, symmetric difference, bi-implication,
Heyting
-/


open Function OrderDual

variable {Î¹ Î± Î² : Type*} {Ï€ : Î¹ â†’ Type*}

/-- The symmetric difference operator on a type with `âŠ”` and `\` is `(A \ B) âŠ” (B \ A)`. -/
def symmDiff [Sup Î±] [SDiff Î±] (a b : Î±) : Î± :=
  a \ b âŠ” b \ a
#align symm_diff symmDiff

/-- The Heyting bi-implication is `(b â‡¨ a) âŠ“ (a â‡¨ b)`. This generalizes equivalence of
propositions. -/
def bihimp [Inf Î±] [HImp Î±] (a b : Î±) : Î± :=
  (b â‡¨ a) âŠ“ (a â‡¨ b)
#align bihimp bihimp

/- This notation might conflict with the Laplacian once we have it. Feel free to put it in locale
  `order` or `symmDiff` if that happens. -/
/-- Notation for symmDiff -/
infixl:100 " âˆ† " => symmDiff

/-- Notation for bihimp -/
infixl:100 " â‡” " => bihimp

theorem symmDiff_def [Sup Î±] [SDiff Î±] (a b : Î±) : a âˆ† b = a \ b âŠ” b \ a :=
  rfl
#align symm_diff_def symmDiff_def

theorem bihimp_def [Inf Î±] [HImp Î±] (a b : Î±) : a â‡” b = (b â‡¨ a) âŠ“ (a â‡¨ b) :=
  rfl
#align bihimp_def bihimp_def

theorem symmDiff_eq_Xor' (p q : Prop) : p âˆ† q = Xor' p q :=
  rfl
#align symm_diff_eq_xor symmDiff_eq_Xor'

@[simp]
theorem bihimp_iff_iff {p q : Prop} : p â‡” q â†” (p â†” q) :=
  (iff_iff_implies_and_implies _ _).symm.trans Iff.comm
#align bihimp_iff_iff bihimp_iff_iff

@[simp]
theorem Bool.symmDiff_eq_xor : âˆ€ p q : Bool, p âˆ† q = xor p q := by decide
                                                                   -- ðŸŽ‰ no goals
#align bool.symm_diff_eq_bxor Bool.symmDiff_eq_xor

section GeneralizedCoheytingAlgebra

variable [GeneralizedCoheytingAlgebra Î±] (a b c d : Î±)

@[simp]
theorem toDual_symmDiff : toDual (a âˆ† b) = toDual a â‡” toDual b :=
  rfl
#align to_dual_symm_diff toDual_symmDiff

@[simp]
theorem ofDual_bihimp (a b : Î±áµ’áµˆ) : ofDual (a â‡” b) = ofDual a âˆ† ofDual b :=
  rfl
#align of_dual_bihimp ofDual_bihimp

theorem symmDiff_comm : a âˆ† b = b âˆ† a := by simp only [symmDiff, sup_comm]
                                            -- ðŸŽ‰ no goals
#align symm_diff_comm symmDiff_comm

instance symmDiff_isCommutative : IsCommutative Î± (Â· âˆ† Â·) :=
  âŸ¨symmDiff_commâŸ©
#align symm_diff_is_comm symmDiff_isCommutative

@[simp]
theorem symmDiff_self : a âˆ† a = âŠ¥ := by rw [symmDiff, sup_idem, sdiff_self]
                                        -- ðŸŽ‰ no goals
#align symm_diff_self symmDiff_self

@[simp]
theorem symmDiff_bot : a âˆ† âŠ¥ = a := by rw [symmDiff, sdiff_bot, bot_sdiff, sup_bot_eq]
                                       -- ðŸŽ‰ no goals
#align symm_diff_bot symmDiff_bot

@[simp]
theorem bot_symmDiff : âŠ¥ âˆ† a = a := by rw [symmDiff_comm, symmDiff_bot]
                                       -- ðŸŽ‰ no goals
#align bot_symm_diff bot_symmDiff

@[simp]
theorem symmDiff_eq_bot {a b : Î±} : a âˆ† b = âŠ¥ â†” a = b := by
  simp_rw [symmDiff, sup_eq_bot_iff, sdiff_eq_bot_iff, le_antisymm_iff]
  -- ðŸŽ‰ no goals
#align symm_diff_eq_bot symmDiff_eq_bot

theorem symmDiff_of_le {a b : Î±} (h : a â‰¤ b) : a âˆ† b = b \ a := by
  rw [symmDiff, sdiff_eq_bot_iff.2 h, bot_sup_eq]
  -- ðŸŽ‰ no goals
#align symm_diff_of_le symmDiff_of_le

theorem symmDiff_of_ge {a b : Î±} (h : b â‰¤ a) : a âˆ† b = a \ b := by
  rw [symmDiff, sdiff_eq_bot_iff.2 h, sup_bot_eq]
  -- ðŸŽ‰ no goals
#align symm_diff_of_ge symmDiff_of_ge

theorem symmDiff_le {a b c : Î±} (ha : a â‰¤ b âŠ” c) (hb : b â‰¤ a âŠ” c) : a âˆ† b â‰¤ c :=
  sup_le (sdiff_le_iff.2 ha) <| sdiff_le_iff.2 hb
#align symm_diff_le symmDiff_le

theorem symmDiff_le_iff {a b c : Î±} : a âˆ† b â‰¤ c â†” a â‰¤ b âŠ” c âˆ§ b â‰¤ a âŠ” c := by
  simp_rw [symmDiff, sup_le_iff, sdiff_le_iff]
  -- ðŸŽ‰ no goals
#align symm_diff_le_iff symmDiff_le_iff

@[simp]
theorem symmDiff_le_sup {a b : Î±} : a âˆ† b â‰¤ a âŠ” b :=
  sup_le_sup sdiff_le sdiff_le
#align symm_diff_le_sup symmDiff_le_sup

theorem symmDiff_eq_sup_sdiff_inf : a âˆ† b = (a âŠ” b) \ (a âŠ“ b) := by simp [sup_sdiff, symmDiff]
                                                                    -- ðŸŽ‰ no goals
#align symm_diff_eq_sup_sdiff_inf symmDiff_eq_sup_sdiff_inf

theorem Disjoint.symmDiff_eq_sup {a b : Î±} (h : Disjoint a b) : a âˆ† b = a âŠ” b := by
  rw [symmDiff, h.sdiff_eq_left, h.sdiff_eq_right]
  -- ðŸŽ‰ no goals
#align disjoint.symm_diff_eq_sup Disjoint.symmDiff_eq_sup

theorem symmDiff_sdiff : a âˆ† b \ c = a \ (b âŠ” c) âŠ” b \ (a âŠ” c) := by
  rw [symmDiff, sup_sdiff_distrib, sdiff_sdiff_left, sdiff_sdiff_left]
  -- ðŸŽ‰ no goals
#align symm_diff_sdiff symmDiff_sdiff

@[simp]
theorem symmDiff_sdiff_inf : a âˆ† b \ (a âŠ“ b) = a âˆ† b := by
  rw [symmDiff_sdiff]
  -- âŠ¢ a \ (b âŠ” a âŠ“ b) âŠ” b \ (a âŠ” a âŠ“ b) = a âˆ† b
  simp [symmDiff]
  -- ðŸŽ‰ no goals
#align symm_diff_sdiff_inf symmDiff_sdiff_inf

@[simp]
theorem symmDiff_sdiff_eq_sup : a âˆ† (b \ a) = a âŠ” b := by
  rw [symmDiff, sdiff_idem]
  -- âŠ¢ a \ (b \ a) âŠ” b \ a = a âŠ” b
  exact
    le_antisymm (sup_le_sup sdiff_le sdiff_le)
      (sup_le le_sdiff_sup <| le_sdiff_sup.trans <| sup_le le_sup_right le_sdiff_sup)
#align symm_diff_sdiff_eq_sup symmDiff_sdiff_eq_sup

@[simp]
theorem sdiff_symmDiff_eq_sup : (a \ b) âˆ† b = a âŠ” b := by
  rw [symmDiff_comm, symmDiff_sdiff_eq_sup, sup_comm]
  -- ðŸŽ‰ no goals
#align sdiff_symm_diff_eq_sup sdiff_symmDiff_eq_sup

@[simp]
theorem symmDiff_sup_inf : a âˆ† b âŠ” a âŠ“ b = a âŠ” b := by
  refine' le_antisymm (sup_le symmDiff_le_sup inf_le_sup) _
  -- âŠ¢ a âŠ” b â‰¤ a âˆ† b âŠ” a âŠ“ b
  rw [sup_inf_left, symmDiff]
  -- âŠ¢ a âŠ” b â‰¤ (a \ b âŠ” b \ a âŠ” a) âŠ“ (a \ b âŠ” b \ a âŠ” b)
  refine' sup_le (le_inf le_sup_right _) (le_inf _ le_sup_right)
  -- âŠ¢ a â‰¤ a \ b âŠ” b \ a âŠ” b
  Â· rw [sup_right_comm]
    -- âŠ¢ a â‰¤ a \ b âŠ” b âŠ” b \ a
    exact le_sup_of_le_left le_sdiff_sup
    -- ðŸŽ‰ no goals
  Â· rw [sup_assoc]
    -- âŠ¢ b â‰¤ a \ b âŠ” (b \ a âŠ” a)
    exact le_sup_of_le_right le_sdiff_sup
    -- ðŸŽ‰ no goals
#align symm_diff_sup_inf symmDiff_sup_inf

@[simp]
theorem inf_sup_symmDiff : a âŠ“ b âŠ” a âˆ† b = a âŠ” b := by rw [sup_comm, symmDiff_sup_inf]
                                                       -- ðŸŽ‰ no goals
#align inf_sup_symm_diff inf_sup_symmDiff

@[simp]
theorem symmDiff_symmDiff_inf : a âˆ† b âˆ† (a âŠ“ b) = a âŠ” b := by
  rw [â† symmDiff_sdiff_inf a, sdiff_symmDiff_eq_sup, symmDiff_sup_inf]
  -- ðŸŽ‰ no goals
#align symm_diff_symm_diff_inf symmDiff_symmDiff_inf

@[simp]
theorem inf_symmDiff_symmDiff : (a âŠ“ b) âˆ† (a âˆ† b) = a âŠ” b := by
  rw [symmDiff_comm, symmDiff_symmDiff_inf]
  -- ðŸŽ‰ no goals
#align inf_symm_diff_symm_diff inf_symmDiff_symmDiff

theorem symmDiff_triangle : a âˆ† c â‰¤ a âˆ† b âŠ” b âˆ† c := by
  refine' (sup_le_sup (sdiff_triangle a b c) <| sdiff_triangle _ b _).trans_eq _
  -- âŠ¢ a \ b âŠ” b \ c âŠ” (c \ b âŠ” b \ a) = a âˆ† b âŠ” b âˆ† c
  rw [@sup_comm _ _ (c \ b), sup_sup_sup_comm, symmDiff, symmDiff]
  -- ðŸŽ‰ no goals
#align symm_diff_triangle symmDiff_triangle

end GeneralizedCoheytingAlgebra

section GeneralizedHeytingAlgebra

variable [GeneralizedHeytingAlgebra Î±] (a b c d : Î±)

@[simp]
theorem toDual_bihimp : toDual (a â‡” b) = toDual a âˆ† toDual b :=
  rfl
#align to_dual_bihimp toDual_bihimp

@[simp]
theorem ofDual_symmDiff (a b : Î±áµ’áµˆ) : ofDual (a âˆ† b) = ofDual a â‡” ofDual b :=
  rfl
#align of_dual_symm_diff ofDual_symmDiff

theorem bihimp_comm : a â‡” b = b â‡” a := by simp only [(Â· â‡” Â·), inf_comm]
                                          -- ðŸŽ‰ no goals
#align bihimp_comm bihimp_comm

instance bihimp_isCommutative : IsCommutative Î± (Â· â‡” Â·) :=
  âŸ¨bihimp_commâŸ©
#align bihimp_is_comm bihimp_isCommutative

@[simp]
theorem bihimp_self : a â‡” a = âŠ¤ := by rw [bihimp, inf_idem, himp_self]
                                      -- ðŸŽ‰ no goals
#align bihimp_self bihimp_self

@[simp]
theorem bihimp_top : a â‡” âŠ¤ = a := by rw [bihimp, himp_top, top_himp, inf_top_eq]
                                     -- ðŸŽ‰ no goals
#align bihimp_top bihimp_top

@[simp]
theorem top_bihimp : âŠ¤ â‡” a = a := by rw [bihimp_comm, bihimp_top]
                                     -- ðŸŽ‰ no goals
#align top_bihimp top_bihimp

@[simp]
theorem bihimp_eq_top {a b : Î±} : a â‡” b = âŠ¤ â†” a = b :=
  @symmDiff_eq_bot Î±áµ’áµˆ _ _ _
#align bihimp_eq_top bihimp_eq_top

theorem bihimp_of_le {a b : Î±} (h : a â‰¤ b) : a â‡” b = b â‡¨ a := by
  rw [bihimp, himp_eq_top_iff.2 h, inf_top_eq]
  -- ðŸŽ‰ no goals
#align bihimp_of_le bihimp_of_le

theorem bihimp_of_ge {a b : Î±} (h : b â‰¤ a) : a â‡” b = a â‡¨ b := by
  rw [bihimp, himp_eq_top_iff.2 h, top_inf_eq]
  -- ðŸŽ‰ no goals
#align bihimp_of_ge bihimp_of_ge

theorem le_bihimp {a b c : Î±} (hb : a âŠ“ b â‰¤ c) (hc : a âŠ“ c â‰¤ b) : a â‰¤ b â‡” c :=
  le_inf (le_himp_iff.2 hc) <| le_himp_iff.2 hb
#align le_bihimp le_bihimp

theorem le_bihimp_iff {a b c : Î±} : a â‰¤ b â‡” c â†” a âŠ“ b â‰¤ c âˆ§ a âŠ“ c â‰¤ b := by
  simp_rw [bihimp, le_inf_iff, le_himp_iff, and_comm]
  -- ðŸŽ‰ no goals
#align le_bihimp_iff le_bihimp_iff

@[simp]
theorem inf_le_bihimp {a b : Î±} : a âŠ“ b â‰¤ a â‡” b :=
  inf_le_inf le_himp le_himp
#align inf_le_bihimp inf_le_bihimp

theorem bihimp_eq_inf_himp_inf : a â‡” b = a âŠ” b â‡¨ a âŠ“ b := by simp [himp_inf_distrib, bihimp]
                                                             -- ðŸŽ‰ no goals
#align bihimp_eq_inf_himp_inf bihimp_eq_inf_himp_inf

theorem Codisjoint.bihimp_eq_inf {a b : Î±} (h : Codisjoint a b) : a â‡” b = a âŠ“ b := by
  rw [bihimp, h.himp_eq_left, h.himp_eq_right]
  -- ðŸŽ‰ no goals
#align codisjoint.bihimp_eq_inf Codisjoint.bihimp_eq_inf

theorem himp_bihimp : a â‡¨ b â‡” c = (a âŠ“ c â‡¨ b) âŠ“ (a âŠ“ b â‡¨ c) := by
  rw [bihimp, himp_inf_distrib, himp_himp, himp_himp]
  -- ðŸŽ‰ no goals
#align himp_bihimp himp_bihimp

@[simp]
theorem sup_himp_bihimp : a âŠ” b â‡¨ a â‡” b = a â‡” b := by
  rw [himp_bihimp]
  -- âŠ¢ ((a âŠ” b) âŠ“ b â‡¨ a) âŠ“ ((a âŠ” b) âŠ“ a â‡¨ b) = a â‡” b
  simp [bihimp]
  -- ðŸŽ‰ no goals
#align sup_himp_bihimp sup_himp_bihimp

@[simp]
theorem bihimp_himp_eq_inf : a â‡” (a â‡¨ b) = a âŠ“ b :=
  @symmDiff_sdiff_eq_sup Î±áµ’áµˆ _ _ _
#align bihimp_himp_eq_inf bihimp_himp_eq_inf

@[simp]
theorem himp_bihimp_eq_inf : (b â‡¨ a) â‡” b = a âŠ“ b :=
  @sdiff_symmDiff_eq_sup Î±áµ’áµˆ _ _ _
#align himp_bihimp_eq_inf himp_bihimp_eq_inf

@[simp]
theorem bihimp_inf_sup : a â‡” b âŠ“ (a âŠ” b) = a âŠ“ b :=
  @symmDiff_sup_inf Î±áµ’áµˆ _ _ _
#align bihimp_inf_sup bihimp_inf_sup

@[simp]
theorem sup_inf_bihimp : (a âŠ” b) âŠ“ a â‡” b = a âŠ“ b :=
  @inf_sup_symmDiff Î±áµ’áµˆ _ _ _
#align sup_inf_bihimp sup_inf_bihimp

@[simp]
theorem bihimp_bihimp_sup : a â‡” b â‡” (a âŠ” b) = a âŠ“ b :=
  @symmDiff_symmDiff_inf Î±áµ’áµˆ _ _ _
#align bihimp_bihimp_sup bihimp_bihimp_sup

@[simp]
theorem sup_bihimp_bihimp : (a âŠ” b) â‡” (a â‡” b) = a âŠ“ b :=
  @inf_symmDiff_symmDiff Î±áµ’áµˆ _ _ _
#align sup_bihimp_bihimp sup_bihimp_bihimp

theorem bihimp_triangle : a â‡” b âŠ“ b â‡” c â‰¤ a â‡” c :=
  @symmDiff_triangle Î±áµ’áµˆ _ _ _ _
#align bihimp_triangle bihimp_triangle

end GeneralizedHeytingAlgebra

section CoheytingAlgebra

variable [CoheytingAlgebra Î±] (a : Î±)

@[simp]
theorem symmDiff_top' : a âˆ† âŠ¤ = ï¿¢a := by simp [symmDiff]
                                         -- ðŸŽ‰ no goals
#align symm_diff_top' symmDiff_top'

@[simp]
theorem top_symmDiff' : âŠ¤ âˆ† a = ï¿¢a := by simp [symmDiff]
                                         -- ðŸŽ‰ no goals
#align top_symm_diff' top_symmDiff'

@[simp]
theorem hnot_symmDiff_self : (ï¿¢a) âˆ† a = âŠ¤ := by
  rw [eq_top_iff, symmDiff, hnot_sdiff, sup_sdiff_self]
  -- âŠ¢ âŠ¤ â‰¤ ï¿¢a âŠ” a
  exact Codisjoint.top_le codisjoint_hnot_left
  -- ðŸŽ‰ no goals
#align hnot_symm_diff_self hnot_symmDiff_self

@[simp]
theorem symmDiff_hnot_self : a âˆ† (ï¿¢a) = âŠ¤ := by rw [symmDiff_comm, hnot_symmDiff_self]
                                                -- ðŸŽ‰ no goals
#align symm_diff_hnot_self symmDiff_hnot_self

theorem IsCompl.symmDiff_eq_top {a b : Î±} (h : IsCompl a b) : a âˆ† b = âŠ¤ := by
  rw [h.eq_hnot, hnot_symmDiff_self]
  -- ðŸŽ‰ no goals
#align is_compl.symm_diff_eq_top IsCompl.symmDiff_eq_top

end CoheytingAlgebra

section HeytingAlgebra

variable [HeytingAlgebra Î±] (a : Î±)

@[simp]
theorem bihimp_bot : a â‡” âŠ¥ = aá¶œ := by simp [bihimp]
                                      -- ðŸŽ‰ no goals
#align bihimp_bot bihimp_bot

@[simp]
theorem bot_bihimp : âŠ¥ â‡” a = aá¶œ := by simp [bihimp]
                                      -- ðŸŽ‰ no goals
#align bot_bihimp bot_bihimp

@[simp]
theorem compl_bihimp_self : aá¶œ â‡” a = âŠ¥ :=
  @hnot_symmDiff_self Î±áµ’áµˆ _ _
#align compl_bihimp_self compl_bihimp_self

@[simp]
theorem bihimp_hnot_self : a â‡” aá¶œ = âŠ¥ :=
  @symmDiff_hnot_self Î±áµ’áµˆ _ _
#align bihimp_hnot_self bihimp_hnot_self

theorem IsCompl.bihimp_eq_bot {a b : Î±} (h : IsCompl a b) : a â‡” b = âŠ¥ := by
  rw [h.eq_compl, compl_bihimp_self]
  -- ðŸŽ‰ no goals
#align is_compl.bihimp_eq_bot IsCompl.bihimp_eq_bot

end HeytingAlgebra

section GeneralizedBooleanAlgebra

variable [GeneralizedBooleanAlgebra Î±] (a b c d : Î±)

@[simp]
theorem sup_sdiff_symmDiff : (a âŠ” b) \ a âˆ† b = a âŠ“ b :=
  sdiff_eq_symm inf_le_sup (by rw [symmDiff_eq_sup_sdiff_inf])
                               -- ðŸŽ‰ no goals
#align sup_sdiff_symm_diff sup_sdiff_symmDiff

theorem disjoint_symmDiff_inf : Disjoint (a âˆ† b) (a âŠ“ b) := by
  rw [symmDiff_eq_sup_sdiff_inf]
  -- âŠ¢ Disjoint ((a âŠ” b) \ (a âŠ“ b)) (a âŠ“ b)
  exact disjoint_sdiff_self_left
  -- ðŸŽ‰ no goals
#align disjoint_symm_diff_inf disjoint_symmDiff_inf

theorem inf_symmDiff_distrib_left : a âŠ“ b âˆ† c = (a âŠ“ b) âˆ† (a âŠ“ c) := by
  rw [symmDiff_eq_sup_sdiff_inf, inf_sdiff_distrib_left, inf_sup_left, inf_inf_distrib_left,
    symmDiff_eq_sup_sdiff_inf]
#align inf_symm_diff_distrib_left inf_symmDiff_distrib_left

theorem inf_symmDiff_distrib_right : a âˆ† b âŠ“ c = (a âŠ“ c) âˆ† (b âŠ“ c) := by
  simp_rw [@inf_comm _ _ _ c, inf_symmDiff_distrib_left]
  -- ðŸŽ‰ no goals
#align inf_symm_diff_distrib_right inf_symmDiff_distrib_right

theorem sdiff_symmDiff : c \ a âˆ† b = c âŠ“ a âŠ“ b âŠ” c \ a âŠ“ c \ b := by
  simp only [(Â· âˆ† Â·), sdiff_sdiff_sup_sdiff']
  -- ðŸŽ‰ no goals
#align sdiff_symm_diff sdiff_symmDiff

theorem sdiff_symmDiff' : c \ a âˆ† b = c âŠ“ a âŠ“ b âŠ” c \ (a âŠ” b) := by
  rw [sdiff_symmDiff, sdiff_sup, sup_comm]
  -- ðŸŽ‰ no goals
#align sdiff_symm_diff' sdiff_symmDiff'

@[simp]
theorem symmDiff_sdiff_left : a âˆ† b \ a = b \ a := by
  rw [symmDiff_def, sup_sdiff, sdiff_idem, sdiff_sdiff_self, bot_sup_eq]
  -- ðŸŽ‰ no goals
#align symm_diff_sdiff_left symmDiff_sdiff_left

@[simp]
theorem symmDiff_sdiff_right : a âˆ† b \ b = a \ b := by rw [symmDiff_comm, symmDiff_sdiff_left]
                                                       -- ðŸŽ‰ no goals
#align symm_diff_sdiff_right symmDiff_sdiff_right

@[simp]
theorem sdiff_symmDiff_left : a \ a âˆ† b = a âŠ“ b := by simp [sdiff_symmDiff]
                                                      -- ðŸŽ‰ no goals
#align sdiff_symm_diff_left sdiff_symmDiff_left

@[simp]
theorem sdiff_symmDiff_right : b \ a âˆ† b = a âŠ“ b := by
  rw [symmDiff_comm, inf_comm, sdiff_symmDiff_left]
  -- ðŸŽ‰ no goals
#align sdiff_symm_diff_right sdiff_symmDiff_right

theorem symmDiff_eq_sup : a âˆ† b = a âŠ” b â†” Disjoint a b := by
  refine' âŸ¨fun h => _, Disjoint.symmDiff_eq_supâŸ©
  -- âŠ¢ Disjoint a b
  rw [symmDiff_eq_sup_sdiff_inf, sdiff_eq_self_iff_disjoint] at h
  -- âŠ¢ Disjoint a b
  exact h.of_disjoint_inf_of_le le_sup_left
  -- ðŸŽ‰ no goals
#align symm_diff_eq_sup symmDiff_eq_sup

@[simp]
theorem le_symmDiff_iff_left : a â‰¤ a âˆ† b â†” Disjoint a b := by
  refine' âŸ¨fun h => _, fun h => h.symmDiff_eq_sup.symm â–¸ le_sup_leftâŸ©
  -- âŠ¢ Disjoint a b
  rw [symmDiff_eq_sup_sdiff_inf] at h
  -- âŠ¢ Disjoint a b
  exact disjoint_iff_inf_le.mpr (le_sdiff_iff.1 <| inf_le_of_left_le h).le
  -- ðŸŽ‰ no goals
#align le_symm_diff_iff_left le_symmDiff_iff_left

@[simp]
theorem le_symmDiff_iff_right : b â‰¤ a âˆ† b â†” Disjoint a b := by
  rw [symmDiff_comm, le_symmDiff_iff_left, disjoint_comm]
  -- ðŸŽ‰ no goals
#align le_symm_diff_iff_right le_symmDiff_iff_right

theorem symmDiff_symmDiff_left :
    a âˆ† b âˆ† c = a \ (b âŠ” c) âŠ” b \ (a âŠ” c) âŠ” c \ (a âŠ” b) âŠ” a âŠ“ b âŠ“ c :=
  calc
    a âˆ† b âˆ† c = a âˆ† b \ c âŠ” c \ a âˆ† b := symmDiff_def _ _
    _ = a \ (b âŠ” c) âŠ” b \ (a âŠ” c) âŠ” (c \ (a âŠ” b) âŠ” c âŠ“ a âŠ“ b) := by
        { rw [sdiff_symmDiff', @sup_comm _ _ (c âŠ“ a âŠ“ b), symmDiff_sdiff] }
        -- ðŸŽ‰ no goals
    _ = a \ (b âŠ” c) âŠ” b \ (a âŠ” c) âŠ” c \ (a âŠ” b) âŠ” a âŠ“ b âŠ“ c := by ac_rfl
                                                                  -- ðŸŽ‰ no goals
#align symm_diff_symm_diff_left symmDiff_symmDiff_left

theorem symmDiff_symmDiff_right :
    a âˆ† (b âˆ† c) = a \ (b âŠ” c) âŠ” b \ (a âŠ” c) âŠ” c \ (a âŠ” b) âŠ” a âŠ“ b âŠ“ c :=
  calc
    a âˆ† (b âˆ† c) = a \ b âˆ† c âŠ” b âˆ† c \ a := symmDiff_def _ _
    _ = a \ (b âŠ” c) âŠ” a âŠ“ b âŠ“ c âŠ” (b \ (c âŠ” a) âŠ” c \ (b âŠ” a)) := by
        { rw [sdiff_symmDiff', @sup_comm _ _ (a âŠ“ b âŠ“ c), symmDiff_sdiff] }
        -- ðŸŽ‰ no goals
    _ = a \ (b âŠ” c) âŠ” b \ (a âŠ” c) âŠ” c \ (a âŠ” b) âŠ” a âŠ“ b âŠ“ c := by ac_rfl
                                                                  -- ðŸŽ‰ no goals
#align symm_diff_symm_diff_right symmDiff_symmDiff_right

theorem symmDiff_assoc : a âˆ† b âˆ† c = a âˆ† (b âˆ† c) := by
  rw [symmDiff_symmDiff_left, symmDiff_symmDiff_right]
  -- ðŸŽ‰ no goals
#align symm_diff_assoc symmDiff_assoc

instance symmDiff_isAssociative : IsAssociative Î± (Â· âˆ† Â·) :=
  âŸ¨symmDiff_assocâŸ©
#align symm_diff_is_assoc symmDiff_isAssociative

theorem symmDiff_left_comm : a âˆ† (b âˆ† c) = b âˆ† (a âˆ† c) := by
  simp_rw [â† symmDiff_assoc, symmDiff_comm]
  -- ðŸŽ‰ no goals
#align symm_diff_left_comm symmDiff_left_comm

theorem symmDiff_right_comm : a âˆ† b âˆ† c = a âˆ† c âˆ† b := by simp_rw [symmDiff_assoc, symmDiff_comm]
                                                          -- ðŸŽ‰ no goals
#align symm_diff_right_comm symmDiff_right_comm

theorem symmDiff_symmDiff_symmDiff_comm : a âˆ† b âˆ† (c âˆ† d) = a âˆ† c âˆ† (b âˆ† d) := by
  simp_rw [symmDiff_assoc, symmDiff_left_comm]
  -- ðŸŽ‰ no goals
#align symm_diff_symm_diff_symm_diff_comm symmDiff_symmDiff_symmDiff_comm

@[simp]
theorem symmDiff_symmDiff_cancel_left : a âˆ† (a âˆ† b) = b := by simp [â† symmDiff_assoc]
                                                              -- ðŸŽ‰ no goals
#align symm_diff_symm_diff_cancel_left symmDiff_symmDiff_cancel_left

@[simp]
theorem symmDiff_symmDiff_cancel_right : b âˆ† a âˆ† a = b := by simp [symmDiff_assoc]
                                                             -- ðŸŽ‰ no goals
#align symm_diff_symm_diff_cancel_right symmDiff_symmDiff_cancel_right

@[simp]
theorem symmDiff_symmDiff_self' : a âˆ† b âˆ† a = b := by
  rw [symmDiff_comm, symmDiff_symmDiff_cancel_left]
  -- ðŸŽ‰ no goals
#align symm_diff_symm_diff_self' symmDiff_symmDiff_self'

theorem symmDiff_left_involutive (a : Î±) : Involutive (Â· âˆ† a) :=
  symmDiff_symmDiff_cancel_right _
#align symm_diff_left_involutive symmDiff_left_involutive

theorem symmDiff_right_involutive (a : Î±) : Involutive ((Â· âˆ† Â·) a) :=
  symmDiff_symmDiff_cancel_left _
#align symm_diff_right_involutive symmDiff_right_involutive

theorem symmDiff_left_injective (a : Î±) : Injective (Â· âˆ† a) :=
  Function.Involutive.injective (symmDiff_left_involutive a)
#align symm_diff_left_injective symmDiff_left_injective

theorem symmDiff_right_injective (a : Î±) : Injective ((Â· âˆ† Â·) a) :=
  Function.Involutive.injective (symmDiff_right_involutive _)
#align symm_diff_right_injective symmDiff_right_injective

theorem symmDiff_left_surjective (a : Î±) : Surjective (Â· âˆ† a) :=
  Function.Involutive.surjective (symmDiff_left_involutive _)
#align symm_diff_left_surjective symmDiff_left_surjective

theorem symmDiff_right_surjective (a : Î±) : Surjective ((Â· âˆ† Â·) a) :=
  Function.Involutive.surjective (symmDiff_right_involutive _)
#align symm_diff_right_surjective symmDiff_right_surjective

variable {a b c}

@[simp]
theorem symmDiff_left_inj : a âˆ† b = c âˆ† b â†” a = c :=
  (symmDiff_left_injective _).eq_iff
#align symm_diff_left_inj symmDiff_left_inj

@[simp]
theorem symmDiff_right_inj : a âˆ† b = a âˆ† c â†” b = c :=
  (symmDiff_right_injective _).eq_iff
#align symm_diff_right_inj symmDiff_right_inj

@[simp]
theorem symmDiff_eq_left : a âˆ† b = a â†” b = âŠ¥ :=
  calc
    a âˆ† b = a â†” a âˆ† b = a âˆ† âŠ¥ := by rw [symmDiff_bot]
                                    -- ðŸŽ‰ no goals
    _ â†” b = âŠ¥ := by rw [symmDiff_right_inj]
                    -- ðŸŽ‰ no goals
#align symm_diff_eq_left symmDiff_eq_left

@[simp]
theorem symmDiff_eq_right : a âˆ† b = b â†” a = âŠ¥ := by rw [symmDiff_comm, symmDiff_eq_left]
                                                    -- ðŸŽ‰ no goals
#align symm_diff_eq_right symmDiff_eq_right

protected theorem Disjoint.symmDiff_left (ha : Disjoint a c) (hb : Disjoint b c) :
    Disjoint (a âˆ† b) c := by
  rw [symmDiff_eq_sup_sdiff_inf]
  -- âŠ¢ Disjoint ((a âŠ” b) \ (a âŠ“ b)) c
  exact (ha.sup_left hb).disjoint_sdiff_left
  -- ðŸŽ‰ no goals
#align disjoint.symm_diff_left Disjoint.symmDiff_left

protected theorem Disjoint.symmDiff_right (ha : Disjoint a b) (hb : Disjoint a c) :
    Disjoint a (b âˆ† c) :=
  (ha.symm.symmDiff_left hb.symm).symm
#align disjoint.symm_diff_right Disjoint.symmDiff_right

theorem symmDiff_eq_iff_sdiff_eq (ha : a â‰¤ c) : a âˆ† b = c â†” c \ a = b := by
  rw [â† symmDiff_of_le ha]
  -- âŠ¢ a âˆ† b = c â†” a âˆ† c = b
  exact ((symmDiff_right_involutive a).toPerm _).apply_eq_iff_eq_symm_apply.trans eq_comm
  -- ðŸŽ‰ no goals
#align symm_diff_eq_iff_sdiff_eq symmDiff_eq_iff_sdiff_eq

end GeneralizedBooleanAlgebra

section BooleanAlgebra

variable [BooleanAlgebra Î±] (a b c d : Î±)

/- `CogeneralizedBooleanAlgebra` isn't actually a typeclass, but the lemmas in here are dual to
the `GeneralizedBooleanAlgebra` ones -/
section CogeneralizedBooleanAlgebra

@[simp]
theorem inf_himp_bihimp : a â‡” b â‡¨ a âŠ“ b = a âŠ” b :=
  @sup_sdiff_symmDiff Î±áµ’áµˆ _ _ _
#align inf_himp_bihimp inf_himp_bihimp

theorem codisjoint_bihimp_sup : Codisjoint (a â‡” b) (a âŠ” b) :=
  @disjoint_symmDiff_inf Î±áµ’áµˆ _ _ _
#align codisjoint_bihimp_sup codisjoint_bihimp_sup

@[simp]
theorem himp_bihimp_left : a â‡¨ a â‡” b = a â‡¨ b :=
  @symmDiff_sdiff_left Î±áµ’áµˆ _ _ _
#align himp_bihimp_left himp_bihimp_left

@[simp]
theorem himp_bihimp_right : b â‡¨ a â‡” b = b â‡¨ a :=
  @symmDiff_sdiff_right Î±áµ’áµˆ _ _ _
#align himp_bihimp_right himp_bihimp_right

@[simp]
theorem bihimp_himp_left : a â‡” b â‡¨ a = a âŠ” b :=
  @sdiff_symmDiff_left Î±áµ’áµˆ _ _ _
#align bihimp_himp_left bihimp_himp_left

@[simp]
theorem bihimp_himp_right : a â‡” b â‡¨ b = a âŠ” b :=
  @sdiff_symmDiff_right Î±áµ’áµˆ _ _ _
#align bihimp_himp_right bihimp_himp_right

@[simp]
theorem bihimp_eq_inf : a â‡” b = a âŠ“ b â†” Codisjoint a b :=
  @symmDiff_eq_sup Î±áµ’áµˆ _ _ _
#align bihimp_eq_inf bihimp_eq_inf

@[simp]
theorem bihimp_le_iff_left : a â‡” b â‰¤ a â†” Codisjoint a b :=
  @le_symmDiff_iff_left Î±áµ’áµˆ _ _ _
#align bihimp_le_iff_left bihimp_le_iff_left

@[simp]
theorem bihimp_le_iff_right : a â‡” b â‰¤ b â†” Codisjoint a b :=
  @le_symmDiff_iff_right Î±áµ’áµˆ _ _ _
#align bihimp_le_iff_right bihimp_le_iff_right

theorem bihimp_assoc : a â‡” b â‡” c = a â‡” (b â‡” c) :=
  @symmDiff_assoc Î±áµ’áµˆ _ _ _ _
#align bihimp_assoc bihimp_assoc

instance bihimp_isAssociative : IsAssociative Î± (Â· â‡” Â·) :=
  âŸ¨bihimp_assocâŸ©
#align bihimp_is_assoc bihimp_isAssociative

theorem bihimp_left_comm : a â‡” (b â‡” c) = b â‡” (a â‡” c) := by simp_rw [â† bihimp_assoc, bihimp_comm]
                                                           -- ðŸŽ‰ no goals
#align bihimp_left_comm bihimp_left_comm

theorem bihimp_right_comm : a â‡” b â‡” c = a â‡” c â‡” b := by simp_rw [bihimp_assoc, bihimp_comm]
                                                        -- ðŸŽ‰ no goals
#align bihimp_right_comm bihimp_right_comm

theorem bihimp_bihimp_bihimp_comm : a â‡” b â‡” (c â‡” d) = a â‡” c â‡” (b â‡” d) := by
  simp_rw [bihimp_assoc, bihimp_left_comm]
  -- ðŸŽ‰ no goals
#align bihimp_bihimp_bihimp_comm bihimp_bihimp_bihimp_comm

@[simp]
theorem bihimp_bihimp_cancel_left : a â‡” (a â‡” b) = b := by simp [â† bihimp_assoc]
                                                          -- ðŸŽ‰ no goals
#align bihimp_bihimp_cancel_left bihimp_bihimp_cancel_left

@[simp]
theorem bihimp_bihimp_cancel_right : b â‡” a â‡” a = b := by simp [bihimp_assoc]
                                                         -- ðŸŽ‰ no goals
#align bihimp_bihimp_cancel_right bihimp_bihimp_cancel_right

@[simp]
theorem bihimp_bihimp_self : a â‡” b â‡” a = b := by rw [bihimp_comm, bihimp_bihimp_cancel_left]
                                                 -- ðŸŽ‰ no goals
#align bihimp_bihimp_self bihimp_bihimp_self

theorem bihimp_left_involutive (a : Î±) : Involutive (Â· â‡” a) :=
  bihimp_bihimp_cancel_right _
#align bihimp_left_involutive bihimp_left_involutive

theorem bihimp_right_involutive (a : Î±) : Involutive ((Â· â‡” Â·) a) :=
  bihimp_bihimp_cancel_left _
#align bihimp_right_involutive bihimp_right_involutive

theorem bihimp_left_injective (a : Î±) : Injective (Â· â‡” a) :=
  @symmDiff_left_injective Î±áµ’áµˆ _ _
#align bihimp_left_injective bihimp_left_injective

theorem bihimp_right_injective (a : Î±) : Injective ((Â· â‡” Â·) a) :=
  @symmDiff_right_injective Î±áµ’áµˆ _ _
#align bihimp_right_injective bihimp_right_injective

theorem bihimp_left_surjective (a : Î±) : Surjective (Â· â‡” a) :=
  @symmDiff_left_surjective Î±áµ’áµˆ _ _
#align bihimp_left_surjective bihimp_left_surjective

theorem bihimp_right_surjective (a : Î±) : Surjective ((Â· â‡” Â·) a) :=
  @symmDiff_right_surjective Î±áµ’áµˆ _ _
#align bihimp_right_surjective bihimp_right_surjective

variable {a b c}

@[simp]
theorem bihimp_left_inj : a â‡” b = c â‡” b â†” a = c :=
  (bihimp_left_injective _).eq_iff
#align bihimp_left_inj bihimp_left_inj

@[simp]
theorem bihimp_right_inj : a â‡” b = a â‡” c â†” b = c :=
  (bihimp_right_injective _).eq_iff
#align bihimp_right_inj bihimp_right_inj

@[simp]
theorem bihimp_eq_left : a â‡” b = a â†” b = âŠ¤ :=
  @symmDiff_eq_left Î±áµ’áµˆ _ _ _
#align bihimp_eq_left bihimp_eq_left

@[simp]
theorem bihimp_eq_right : a â‡” b = b â†” a = âŠ¤ :=
  @symmDiff_eq_right Î±áµ’áµˆ _ _ _
#align bihimp_eq_right bihimp_eq_right

protected theorem Codisjoint.bihimp_left (ha : Codisjoint a c) (hb : Codisjoint b c) :
    Codisjoint (a â‡” b) c :=
  (ha.inf_left hb).mono_left inf_le_bihimp
#align codisjoint.bihimp_left Codisjoint.bihimp_left

protected theorem Codisjoint.bihimp_right (ha : Codisjoint a b) (hb : Codisjoint a c) :
    Codisjoint a (b â‡” c) :=
  (ha.inf_right hb).mono_right inf_le_bihimp
#align codisjoint.bihimp_right Codisjoint.bihimp_right

end CogeneralizedBooleanAlgebra

theorem symmDiff_eq : a âˆ† b = a âŠ“ bá¶œ âŠ” b âŠ“ aá¶œ := by simp only [(Â· âˆ† Â·), sdiff_eq]
                                                    -- ðŸŽ‰ no goals
#align symm_diff_eq symmDiff_eq

theorem bihimp_eq : a â‡” b = (a âŠ” bá¶œ) âŠ“ (b âŠ” aá¶œ) := by simp only [(Â· â‡” Â·), himp_eq]
                                                      -- ðŸŽ‰ no goals
#align bihimp_eq bihimp_eq

theorem symmDiff_eq' : a âˆ† b = (a âŠ” b) âŠ“ (aá¶œ âŠ” bá¶œ) := by
  rw [symmDiff_eq_sup_sdiff_inf, sdiff_eq, compl_inf]
  -- ðŸŽ‰ no goals
#align symm_diff_eq' symmDiff_eq'

theorem bihimp_eq' : a â‡” b = a âŠ“ b âŠ” aá¶œ âŠ“ bá¶œ :=
  @symmDiff_eq' Î±áµ’áµˆ _ _ _
#align bihimp_eq' bihimp_eq'

theorem symmDiff_top : a âˆ† âŠ¤ = aá¶œ :=
  symmDiff_top' _
#align symm_diff_top symmDiff_top

theorem top_symmDiff : âŠ¤ âˆ† a = aá¶œ :=
  top_symmDiff' _
#align top_symm_diff top_symmDiff

@[simp]
theorem compl_symmDiff : (a âˆ† b)á¶œ = a â‡” b := by
  simp_rw [symmDiff, compl_sup_distrib, compl_sdiff, bihimp, inf_comm]
  -- ðŸŽ‰ no goals
#align compl_symm_diff compl_symmDiff

@[simp]
theorem compl_bihimp : (a â‡” b)á¶œ = a âˆ† b :=
  @compl_symmDiff Î±áµ’áµˆ _ _ _
#align compl_bihimp compl_bihimp

@[simp]
theorem compl_symmDiff_compl : aá¶œ âˆ† bá¶œ = a âˆ† b :=
  sup_comm.trans <| by simp_rw [compl_sdiff_compl, sdiff_eq, symmDiff_eq]
                       -- ðŸŽ‰ no goals
#align compl_symm_diff_compl compl_symmDiff_compl

@[simp]
theorem compl_bihimp_compl : aá¶œ â‡” bá¶œ = a â‡” b :=
  @compl_symmDiff_compl Î±áµ’áµˆ _ _ _
#align compl_bihimp_compl compl_bihimp_compl

@[simp]
theorem symmDiff_eq_top : a âˆ† b = âŠ¤ â†” IsCompl a b := by
  rw [symmDiff_eq', â† compl_inf, inf_eq_top_iff, compl_eq_top, isCompl_iff, disjoint_iff,
    codisjoint_iff, and_comm]
#align symm_diff_eq_top symmDiff_eq_top

@[simp]
theorem bihimp_eq_bot : a â‡” b = âŠ¥ â†” IsCompl a b := by
  rw [bihimp_eq', â† compl_sup, sup_eq_bot_iff, compl_eq_bot, isCompl_iff, disjoint_iff,
    codisjoint_iff]
#align bihimp_eq_bot bihimp_eq_bot

@[simp]
theorem compl_symmDiff_self : aá¶œ âˆ† a = âŠ¤ :=
  hnot_symmDiff_self _
#align compl_symm_diff_self compl_symmDiff_self

@[simp]
theorem symmDiff_compl_self : a âˆ† aá¶œ = âŠ¤ :=
  symmDiff_hnot_self _
#align symm_diff_compl_self symmDiff_compl_self

theorem symmDiff_symmDiff_right' :
    a âˆ† (b âˆ† c) = a âŠ“ b âŠ“ c âŠ” a âŠ“ bá¶œ âŠ“ cá¶œ âŠ” aá¶œ âŠ“ b âŠ“ cá¶œ âŠ” aá¶œ âŠ“ bá¶œ âŠ“ c :=
  calc
    a âˆ† (b âˆ† c) = a âŠ“ (b âŠ“ c âŠ” bá¶œ âŠ“ cá¶œ) âŠ” (b âŠ“ cá¶œ âŠ” c âŠ“ bá¶œ) âŠ“ aá¶œ := by
        { rw [symmDiff_eq, compl_symmDiff, bihimp_eq', symmDiff_eq] }
        -- ðŸŽ‰ no goals
    _ = a âŠ“ b âŠ“ c âŠ” a âŠ“ bá¶œ âŠ“ cá¶œ âŠ” b âŠ“ cá¶œ âŠ“ aá¶œ âŠ” c âŠ“ bá¶œ âŠ“ aá¶œ := by
        { rw [inf_sup_left, inf_sup_right, â† sup_assoc, â† inf_assoc, â† inf_assoc] }
        -- ðŸŽ‰ no goals
    _ = a âŠ“ b âŠ“ c âŠ” a âŠ“ bá¶œ âŠ“ cá¶œ âŠ” aá¶œ âŠ“ b âŠ“ cá¶œ âŠ” aá¶œ âŠ“ bá¶œ âŠ“ c := (by
      congr 1
      -- âŠ¢ a âŠ“ b âŠ“ c âŠ” a âŠ“ bá¶œ âŠ“ cá¶œ âŠ” b âŠ“ cá¶œ âŠ“ aá¶œ = a âŠ“ b âŠ“ c âŠ” a âŠ“ bá¶œ âŠ“ cá¶œ âŠ” aá¶œ âŠ“ b âŠ“ cá¶œ
      Â· congr 1
        -- âŠ¢ b âŠ“ cá¶œ âŠ“ aá¶œ = aá¶œ âŠ“ b âŠ“ cá¶œ
        rw [inf_comm, inf_assoc]
        -- ðŸŽ‰ no goals
      Â· apply inf_left_right_swap)
        -- ðŸŽ‰ no goals
#align symm_diff_symm_diff_right' symmDiff_symmDiff_right'

variable {a b c}

theorem Disjoint.le_symmDiff_sup_symmDiff_left (h : Disjoint a b) : c â‰¤ a âˆ† c âŠ” b âˆ† c := by
  trans c \ (a âŠ“ b)
  -- âŠ¢ c â‰¤ c \ (a âŠ“ b)
  Â· rw [h.eq_bot, sdiff_bot]
    -- ðŸŽ‰ no goals
  Â· rw [sdiff_inf]
    -- âŠ¢ c \ a âŠ” c \ b â‰¤ a âˆ† c âŠ” b âˆ† c
    exact sup_le_sup le_sup_right le_sup_right
    -- ðŸŽ‰ no goals
#align disjoint.le_symm_diff_sup_symm_diff_left Disjoint.le_symmDiff_sup_symmDiff_left

theorem Disjoint.le_symmDiff_sup_symmDiff_right (h : Disjoint b c) : a â‰¤ a âˆ† b âŠ” a âˆ† c := by
  simp_rw [symmDiff_comm a]
  -- âŠ¢ a â‰¤ b âˆ† a âŠ” c âˆ† a
  exact h.le_symmDiff_sup_symmDiff_left
  -- ðŸŽ‰ no goals
#align disjoint.le_symm_diff_sup_symm_diff_right Disjoint.le_symmDiff_sup_symmDiff_right

theorem Codisjoint.bihimp_inf_bihimp_le_left (h : Codisjoint a b) : a â‡” c âŠ“ b â‡” c â‰¤ c :=
  h.dual.le_symmDiff_sup_symmDiff_left
#align codisjoint.bihimp_inf_bihimp_le_left Codisjoint.bihimp_inf_bihimp_le_left

theorem Codisjoint.bihimp_inf_bihimp_le_right (h : Codisjoint b c) : a â‡” b âŠ“ a â‡” c â‰¤ a :=
  h.dual.le_symmDiff_sup_symmDiff_right
#align codisjoint.bihimp_inf_bihimp_le_right Codisjoint.bihimp_inf_bihimp_le_right

end BooleanAlgebra

/-! ### Prod -/


section Prod

@[simp]
theorem symmDiff_fst [GeneralizedCoheytingAlgebra Î±] [GeneralizedCoheytingAlgebra Î²]
    (a b : Î± Ã— Î²) : (a âˆ† b).1 = a.1 âˆ† b.1 :=
  rfl
#align symm_diff_fst symmDiff_fst

@[simp]
theorem symmDiff_snd [GeneralizedCoheytingAlgebra Î±] [GeneralizedCoheytingAlgebra Î²]
    (a b : Î± Ã— Î²) : (a âˆ† b).2 = a.2 âˆ† b.2 :=
  rfl
#align symm_diff_snd symmDiff_snd

@[simp]
theorem bihimp_fst [GeneralizedHeytingAlgebra Î±] [GeneralizedHeytingAlgebra Î²] (a b : Î± Ã— Î²) :
    (a â‡” b).1 = a.1 â‡” b.1 :=
  rfl
#align bihimp_fst bihimp_fst

@[simp]
theorem bihimp_snd [GeneralizedHeytingAlgebra Î±] [GeneralizedHeytingAlgebra Î²] (a b : Î± Ã— Î²) :
    (a â‡” b).2 = a.2 â‡” b.2 :=
  rfl
#align bihimp_snd bihimp_snd

end Prod

/-! ### Pi -/


namespace Pi

theorem symmDiff_def [âˆ€ i, GeneralizedCoheytingAlgebra (Ï€ i)] (a b : âˆ€ i, Ï€ i) :
    a âˆ† b = fun i => a i âˆ† b i :=
  rfl
#align pi.symm_diff_def Pi.symmDiff_def

theorem bihimp_def [âˆ€ i, GeneralizedHeytingAlgebra (Ï€ i)] (a b : âˆ€ i, Ï€ i) :
    a â‡” b = fun i => a i â‡” b i :=
  rfl
#align pi.bihimp_def Pi.bihimp_def

@[simp]
theorem symmDiff_apply [âˆ€ i, GeneralizedCoheytingAlgebra (Ï€ i)] (a b : âˆ€ i, Ï€ i) (i : Î¹) :
    (a âˆ† b) i = a i âˆ† b i :=
  rfl
#align pi.symm_diff_apply Pi.symmDiff_apply

@[simp]
theorem bihimp_apply [âˆ€ i, GeneralizedHeytingAlgebra (Ï€ i)] (a b : âˆ€ i, Ï€ i) (i : Î¹) :
    (a â‡” b) i = a i â‡” b i :=
  rfl
#align pi.bihimp_apply Pi.bihimp_apply

end Pi
