/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
module

public import Mathlib.CategoryTheory.FintypeCat
public import Mathlib.Order.Category.PartOrd

/-!
# The category of finite partial orders

This defines `FinPartOrd`, the category of finite partial orders.

Note: `FinPartOrd` is *not* a subcategory of `BddOrd` because finite orders are not necessarily
bounded.

## TODO

`FinPartOrd` is equivalent to a small category.
-/

@[expose] public section


universe u v

open CategoryTheory


/-- The category of finite partial orders with monotone functions. -/
structure FinPartOrd extends PartOrd where
  [isFintype : Fintype toPartOrd]

namespace FinPartOrd

instance : CoeSort FinPartOrd Type* :=
  âŸ¨fun X => X.toPartOrdâŸ©

instance (X : FinPartOrd) : PartialOrder X :=
  X.toPartOrd.str

attribute [instance] FinPartOrd.isFintype

/-- Construct a bundled `FinPartOrd` from `PartialOrder` + `Fintype`. -/
abbrev of (Î± : Type*) [PartialOrder Î±] [Fintype Î±] : FinPartOrd where
  carrier := Î±

instance : Inhabited FinPartOrd :=
  âŸ¨of PUnitâŸ©

instance largeCategory : LargeCategory FinPartOrd :=
  inferInstanceAs (Category (InducedCategory _ FinPartOrd.toPartOrd))

instance concreteCategory : ConcreteCategory FinPartOrd (Â· â†’o Â·) :=
  InducedCategory.concreteCategory FinPartOrd.toPartOrd

instance hasForgetToPartOrd : HasForgetâ‚‚ FinPartOrd PartOrd :=
  InducedCategory.hasForgetâ‚‚ FinPartOrd.toPartOrd

instance hasForgetToFintype : HasForgetâ‚‚ FinPartOrd FintypeCat where
  forgetâ‚‚.obj X := .of X
  forgetâ‚‚.map f := FintypeCat.homMk f.hom

/-- Typecheck a `OrderHom` as a morphism in `FinPartOrd`. -/
abbrev ofHom {X Y : Type u} [PartialOrder X] [Fintype X] [PartialOrder Y] [Fintype Y] (f : X â†’o Y) :
    of X âŸ¶ of Y :=
  ConcreteCategory.ofHom (C := FinPartOrd) f

@[simp]
lemma hom_hom_id {X : FinPartOrd} : (ðŸ™ X : X âŸ¶ X).hom.hom = OrderHom.id := rfl

@[deprecated (since := "2025-12-18")] alias hom_id := hom_hom_id

/- Provided for rewriting. -/
lemma id_apply (X : FinPartOrd) (x : X) :
    (ðŸ™ X : X âŸ¶ X) x = x := by simp

@[simp]
lemma hom_hom_comp {X Y Z : FinPartOrd} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    (f â‰« g).hom.hom = g.hom.hom.comp f.hom.hom := rfl

@[deprecated (since := "2025-12-18")] alias hom_comp := hom_hom_comp

/- Provided for rewriting. -/
lemma comp_apply {X Y Z : FinPartOrd} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (x : X) :
    (f â‰« g) x = g (f x) := by simp

@[ext]
lemma hom_ext {X Y : FinPartOrd} {f g : X âŸ¶ Y} (hf : f.hom.hom = g.hom.hom) : f = g :=
  InducedCategory.hom_ext (ConcreteCategory.ext hf)

@[simp]
lemma hom_hom_ofHom {X Y : Type u} [PartialOrder X] [Fintype X] [PartialOrder Y] [Fintype Y]
    (f : X â†’o Y) :
  (ofHom f).hom.hom = f := rfl

@[deprecated (since := "2025-12-18")] alias hom_ofHom := hom_hom_ofHom

@[simp]
lemma ofHom_hom_hom {X Y : FinPartOrd} (f : X âŸ¶ Y) :
    ofHom f.hom.hom = f := rfl

@[deprecated (since := "2025-12-18")] alias ofHom_hom := ofHom_hom_hom

/-- Constructs an isomorphism of finite partial orders from an order isomorphism between them. -/
@[simps]
def Iso.mk {Î± Î² : FinPartOrd.{u}} (e : Î± â‰ƒo Î²) : Î± â‰… Î² where
  hom := ofHom e
  inv := ofHom e.symm

/-- `OrderDual` as a functor. -/
@[simps map]
def dual : FinPartOrd â¥¤ FinPartOrd where
  obj X := of Xáµ’áµˆ
  map f := ofHom f.hom.hom.dual

/-- The equivalence between `FinPartOrd` and itself induced by `OrderDual` both ways. -/
@[simps]
def dualEquiv : FinPartOrd â‰Œ FinPartOrd where
  functor := dual
  inverse := dual
  unitIso := NatIso.ofComponents fun X => Iso.mk <| OrderIso.dualDual X
  counitIso := NatIso.ofComponents fun X => Iso.mk <| OrderIso.dualDual X

end FinPartOrd

theorem FinPartOrd_dual_comp_forget_to_partOrd :
    FinPartOrd.dual â‹™ forgetâ‚‚ FinPartOrd PartOrd =
      forgetâ‚‚ FinPartOrd PartOrd â‹™ PartOrd.dual := rfl
