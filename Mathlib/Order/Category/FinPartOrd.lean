/-
Copyright (c) 2022 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies
-/
import Mathlib.CategoryTheory.FintypeCat
import Mathlib.Order.Category.PartOrd

/-!
# The category of finite partial orders

This defines `FinPartOrd`, the category of finite partial orders.

Note: `FinPartOrd` is *not* a subcategory of `BddOrd` because finite orders are not necessarily
bounded.

## TODO

`FinPartOrd` is equivalent to a small category.
-/


universe u v

open CategoryTheory


/-- The category of finite partial orders with monotone functions. -/
structure FinPartOrd extends PartOrd where
  [isFintype : Fintype toPartOrd]

namespace FinPartOrd

instance : CoeSort FinPartOrd Type* :=
  ‚ü®fun X ‚Ü¶ X.toPartOrd‚ü©

instance (X : FinPartOrd) : PartialOrder X :=
  X.toPartOrd.str

attribute [instance] FinPartOrd.isFintype

/-- Construct a bundled `FinPartOrd` from `PartialOrder` + `Fintype`. -/
abbrev of (Œ± : Type*) [PartialOrder Œ±] [Fintype Œ±] : FinPartOrd where
  carrier := Œ±

instance : Inhabited FinPartOrd :=
  ‚ü®of PUnit‚ü©

instance largeCategory : LargeCategory FinPartOrd :=
  InducedCategory.category FinPartOrd.toPartOrd

instance concreteCategory : ConcreteCategory FinPartOrd (¬∑ ‚Üío ¬∑) :=
  InducedCategory.concreteCategory FinPartOrd.toPartOrd

instance hasForgetToPartOrd : HasForget‚ÇÇ FinPartOrd PartOrd :=
  InducedCategory.hasForget‚ÇÇ FinPartOrd.toPartOrd

instance hasForgetToFintype : HasForget‚ÇÇ FinPartOrd FintypeCat where
  forget‚ÇÇ.obj X := .of X
  forget‚ÇÇ.map f := f.hom

/-- Typecheck a `OrderHom` as a morphism in `FinPartOrd`. -/
abbrev ofHom {X Y : Type u} [PartialOrder X] [Fintype X] [PartialOrder Y] [Fintype Y] (f : X ‚Üío Y) :
    of X ‚ü∂ of Y :=
  ConcreteCategory.ofHom (C := FinPartOrd) f

@[simp]
lemma hom_id {X : FinPartOrd} : (ùüô X : X ‚ü∂ X).hom = OrderHom.id := rfl

/- Provided for rewriting. -/
lemma id_apply (X : FinPartOrd) (x : X) :
    (ùüô X : X ‚ü∂ X) x = x := by simp

@[simp]
lemma hom_comp {X Y Z : FinPartOrd} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).hom = g.hom.comp f.hom := rfl

/- Provided for rewriting. -/
lemma comp_apply {X Y Z : FinPartOrd} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) :
    (f ‚â´ g) x = g (f x) := by simp

@[ext]
lemma hom_ext {X Y : FinPartOrd} {f g : X ‚ü∂ Y} (hf : f.hom = g.hom) : f = g :=
  ConcreteCategory.ext hf

@[simp]
lemma hom_ofHom {X Y : Type u} [PartialOrder X] [Fintype X] [PartialOrder Y] [Fintype Y]
    (f : X ‚Üío Y) : (ofHom f).hom = f :=
  rfl

@[simp]
lemma ofHom_hom {X Y : FinPartOrd} (f : X ‚ü∂ Y) :
    ofHom f.hom = f := rfl

/-- Constructs an isomorphism of finite partial orders from an order isomorphism between them. -/
@[simps]
def Iso.mk {Œ± Œ≤ : FinPartOrd.{u}} (e : Œ± ‚âÉo Œ≤) : Œ± ‚âÖ Œ≤ where
  hom := ofHom e
  inv := ofHom e.symm

/-- `OrderDual` as a functor. -/
@[simps map]
def dual : FinPartOrd ‚•§ FinPartOrd where
  obj X := of X·µí·µà
  map f := ofHom f.hom.dual

/-- The equivalence between `FinPartOrd` and itself induced by `OrderDual` both ways. -/
@[simps]
def dualEquiv : FinPartOrd ‚âå FinPartOrd where
  functor := dual
  inverse := dual
  unitIso := NatIso.ofComponents fun X ‚Ü¶ Iso.mk <| OrderIso.dualDual X
  counitIso := NatIso.ofComponents fun X ‚Ü¶ Iso.mk <| OrderIso.dualDual X

end FinPartOrd

theorem FinPartOrd_dual_comp_forget_to_partOrd :
    FinPartOrd.dual ‚ãô forget‚ÇÇ FinPartOrd PartOrd =
      forget‚ÇÇ FinPartOrd PartOrd ‚ãô PartOrd.dual := rfl
