/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Order.Category.HeytAlg
import Mathlib.Order.Hom.CompleteLattice

/-!
# The category of boolean algebras

This defines `BoolAlg`, the category of boolean algebras.
-/


open OrderDual Opposite Set

universe u

open CategoryTheory

/-- The category of boolean algebras. -/
structure BoolAlg where
  /-- The underlying boolean algebra. -/
  carrier : Type*
  [str : BooleanAlgebra carrier]

attribute [instance] BoolAlg.str

initialize_simps_projections BoolAlg (carrier â†’ coe, -str)

namespace BoolAlg

instance : CoeSort BoolAlg (Type _) :=
  âŸ¨BoolAlg.carrierâŸ©

attribute [coe] BoolAlg.carrier

/-- Construct a bundled `BoolAlg` from the underlying type and typeclass. -/
abbrev of (X : Type*) [BooleanAlgebra X] : BoolAlg := âŸ¨XâŸ©

/-- The type of morphisms in `BoolAlg R`. -/
@[ext]
structure Hom (X Y : BoolAlg.{u}) where
  private mk ::
  /-- The underlying `BoundedLatticeHom`. -/
  hom' : BoundedLatticeHom X Y

instance : Category BoolAlg.{u} where
  Hom X Y := Hom X Y
  id X := âŸ¨BoundedLatticeHom.id XâŸ©
  comp f g := âŸ¨g.hom'.comp f.hom'âŸ©

instance : ConcreteCategory BoolAlg (BoundedLatticeHom Â· Â·) where
  hom := Hom.hom'
  ofHom := Hom.mk

/-- Turn a morphism in `BoolAlg` back into a `BoundedLatticeHom`. -/
abbrev Hom.hom {X Y : BoolAlg.{u}} (f : Hom X Y) :=
  ConcreteCategory.hom (C := BoolAlg) f

/-- Typecheck a `BoundedLatticeHom` as a morphism in `BoolAlg`. -/
abbrev ofHom {X Y : Type u} [BooleanAlgebra X] [BooleanAlgebra Y] (f : BoundedLatticeHom X Y) :
    of X âŸ¶ of Y :=
  ConcreteCategory.ofHom (C := BoolAlg) f

variable {R} in
/-- Use the `ConcreteCategory.hom` projection for `@[simps]` lemmas. -/
def Hom.Simps.hom (X Y : BoolAlg.{u}) (f : Hom X Y) :=
  f.hom

initialize_simps_projections Hom (hom' â†’ hom)

/-!
The results below duplicate the `ConcreteCategory` simp lemmas, but we can keep them for `dsimp`.
-/

@[simp]
lemma coe_id {X : BoolAlg} : (ðŸ™ X : X â†’ X) = id := rfl

@[simp]
lemma coe_comp {X Y Z : BoolAlg} {f : X âŸ¶ Y} {g : Y âŸ¶ Z} : (f â‰« g : X â†’ Z) = g âˆ˜ f := rfl

@[simp]
lemma forget_map {X Y : BoolAlg} (f : X âŸ¶ Y) :
    (forget BoolAlg).map f = f := rfl

@[ext]
lemma ext {X Y : BoolAlg} {f g : X âŸ¶ Y} (w : âˆ€ x : X, f x = g x) : f = g :=
  ConcreteCategory.hom_ext _ _ w

-- This is not `simp` to avoid rewriting in types of terms.
theorem coe_of (X : Type u) [BooleanAlgebra X] : (BoolAlg.of X : Type u) = X := rfl

@[simp]
lemma hom_id {X : BoolAlg} : (ðŸ™ X : X âŸ¶ X).hom = BoundedLatticeHom.id _ := rfl

/- Provided for rewriting. -/
lemma id_apply (X : BoolAlg) (x : X) :
    (ðŸ™ X : X âŸ¶ X) x = x := by simp

@[simp]
lemma hom_comp {X Y Z : BoolAlg} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    (f â‰« g).hom = g.hom.comp f.hom := rfl

/- Provided for rewriting. -/
lemma comp_apply {X Y Z : BoolAlg} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (x : X) :
    (f â‰« g) x = g (f x) := by simp

@[ext]
lemma hom_ext {X Y : BoolAlg} {f g : X âŸ¶ Y} (hf : f.hom = g.hom) : f = g :=
  Hom.ext hf

@[simp]
lemma hom_ofHom {X Y : Type u} [BooleanAlgebra X] [BooleanAlgebra Y] (f : BoundedLatticeHom X Y) :
  (ofHom f).hom = f := rfl

@[simp]
lemma ofHom_hom {X Y : BoolAlg} (f : X âŸ¶ Y) :
    ofHom (Hom.hom f) = f := rfl

@[simp]
lemma ofHom_id {X : Type u} [BooleanAlgebra X] : ofHom (BoundedLatticeHom.id _) = ðŸ™ (of X) := rfl

@[simp]
lemma ofHom_comp {X Y Z : Type u} [BooleanAlgebra X] [BooleanAlgebra Y] [BooleanAlgebra Z]
    (f : BoundedLatticeHom X Y) (g : BoundedLatticeHom Y Z) :
    ofHom (g.comp f) = ofHom f â‰« ofHom g :=
  rfl

lemma ofHom_apply {X Y : Type u} [BooleanAlgebra X] [BooleanAlgebra Y]
    (f : BoundedLatticeHom X Y) (x : X) :
    (ofHom f) x = f x := rfl

lemma inv_hom_apply {X Y : BoolAlg} (e : X â‰… Y) (x : X) : e.inv (e.hom x) = x := by
  simp

lemma hom_inv_apply {X Y : BoolAlg} (e : X â‰… Y) (s : Y) : e.hom (e.inv s) = s := by
  simp

instance : Inhabited BoolAlg :=
  âŸ¨of PUnitâŸ©

/-- Turn a `BoolAlg` into a `BddDistLat` by forgetting its complement operation. -/
def toBddDistLat (X : BoolAlg) : BddDistLat :=
  .of X

@[simp]
theorem coe_toBddDistLat (X : BoolAlg) : â†¥X.toBddDistLat = â†¥X :=
  rfl

instance hasForgetToBddDistLat : HasForgetâ‚‚ BoolAlg BddDistLat where
  forgetâ‚‚.obj X := .of X
  forgetâ‚‚.map f := BddDistLat.ofHom f.hom

section

attribute [local instance] BoundedLatticeHomClass.toBiheytingHomClass

@[simps]
instance hasForgetToHeytAlg : HasForgetâ‚‚ BoolAlg HeytAlg where
  forgetâ‚‚.obj X := .of X
  forgetâ‚‚.map {X Y} f := HeytAlg.ofHom f.hom

end

/-- Constructs an equivalence between Boolean algebras from an order isomorphism between them. -/
@[simps]
def Iso.mk {Î± Î² : BoolAlg.{u}} (e : Î± â‰ƒo Î²) : Î± â‰… Î² where
  hom := ofHom e
  inv := ofHom e.symm

/-- `OrderDual` as a functor. -/
@[simps map]
def dual : BoolAlg â¥¤ BoolAlg where
  obj X := of Xáµ’áµˆ
  map f := ofHom f.hom.dual

/-- The equivalence between `BoolAlg` and itself induced by `OrderDual` both ways. -/
@[simps functor inverse]
def dualEquiv : BoolAlg â‰Œ BoolAlg where
  functor := dual
  inverse := dual
  unitIso := NatIso.ofComponents fun X => Iso.mk <| OrderIso.dualDual X
  counitIso := NatIso.ofComponents fun X => Iso.mk <| OrderIso.dualDual X

end BoolAlg

theorem boolAlg_dual_comp_forget_to_bddDistLat :
    BoolAlg.dual â‹™ forgetâ‚‚ BoolAlg BddDistLat =
    forgetâ‚‚ BoolAlg BddDistLat â‹™ BddDistLat.dual :=
  rfl

/-- The powerset functor. `Set` as a contravariant functor. -/
@[simps]
def typeToBoolAlgOp : Type u â¥¤ BoolAlgáµ’áµ– where
  obj X := op <| .of (Set X)
  map {X Y} f := Quiver.Hom.op (BoolAlg.ofHom (CompleteLatticeHom.setPreimage f))
