/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Data.Fintype.Order
import Mathlib.Order.Category.BddDistLat
import Mathlib.Order.Category.FinPartOrd

/-!
# The category of finite bounded distributive lattices

This file defines `FinBddDistLat`, the category of finite distributive lattices with
bounded lattice homomorphisms.
-/


universe u

open CategoryTheory

/-- The category of finite distributive lattices with bounded lattice morphisms. -/
structure FinBddDistLat extends BddDistLat where
  [isFintype : Fintype carrier]

namespace FinBddDistLat

instance : CoeSort FinBddDistLat Type* :=
  âŸ¨fun X => X.carrierâŸ©

instance (X : FinBddDistLat) : DistribLattice X :=
  X.str

instance (X : FinBddDistLat) : BoundedOrder X :=
  X.isBoundedOrder

attribute [instance] FinBddDistLat.isFintype

/-- Construct a bundled `FinBddDistLat` from a `Fintype` `BoundedOrder` `DistribLattice`. -/
abbrev of (Î± : Type*) [DistribLattice Î±] [BoundedOrder Î±] [Fintype Î±] : FinBddDistLat where
  carrier := Î±

/-- Construct a bundled `FinBddDistLat` from a `Nonempty` `Fintype` `DistribLattice`. -/
abbrev of' (Î± : Type*) [DistribLattice Î±] [Fintype Î±] [Nonempty Î±] : FinBddDistLat where
  carrier := Î±
  isBoundedOrder := Fintype.toBoundedOrder Î±

/-- The type of morphisms in `FinBddDistLat R`. -/
@[ext]
structure Hom (X Y : FinBddDistLat.{u}) where
  private mk ::
  /-- The underlying `BoundedLatticeHom`. -/
  hom' : BoundedLatticeHom X Y

instance : Category FinBddDistLat.{u} where
  Hom X Y := Hom X Y
  id X := âŸ¨BoundedLatticeHom.id XâŸ©
  comp f g := âŸ¨g.hom'.comp f.hom'âŸ©

instance : ConcreteCategory FinBddDistLat (BoundedLatticeHom Â· Â·) where
  hom := Hom.hom'
  ofHom := Hom.mk

/-- Turn a morphism in `FinBddDistLat` back into a `BoundedLatticeHom`. -/
abbrev Hom.hom {X Y : FinBddDistLat.{u}} (f : Hom X Y) :=
  ConcreteCategory.hom (C := FinBddDistLat) f

/-- Typecheck a `BoundedLatticeHom` as a morphism in `FinBddDistLat`. -/
abbrev ofHom {X Y : Type u} [DistribLattice X] [BoundedOrder X] [Fintype X] [DistribLattice Y]
    [BoundedOrder Y] [Fintype Y]
    (f : BoundedLatticeHom X Y) :
    of X âŸ¶ of Y :=
  ConcreteCategory.ofHom (C := FinBddDistLat) f

variable {R} in
/-- Use the `ConcreteCategory.hom` projection for `@[simps]` lemmas. -/
def Hom.Simps.hom (X Y : FinBddDistLat.{u}) (f : Hom X Y) :=
  f.hom

initialize_simps_projections Hom (hom' â†’ hom)

/-!
The results below duplicate the `ConcreteCategory` simp lemmas, but we can keep them for `dsimp`.
-/

@[simp]
lemma coe_id {X : FinBddDistLat} : (ðŸ™ X : X â†’ X) = id := rfl

@[simp]
lemma coe_comp {X Y Z : FinBddDistLat} {f : X âŸ¶ Y} {g : Y âŸ¶ Z} : (f â‰« g : X â†’ Z) = g âˆ˜ f := rfl

@[simp]
lemma forget_map {X Y : FinBddDistLat} (f : X âŸ¶ Y) :
    (forget FinBddDistLat).map f = f := rfl

@[ext]
lemma ext {X Y : FinBddDistLat} {f g : X âŸ¶ Y} (w : âˆ€ x : X, f x = g x) : f = g :=
  ConcreteCategory.hom_ext _ _ w

@[simp]
lemma hom_id {X : FinBddDistLat} : (ðŸ™ X : X âŸ¶ X).hom = BoundedLatticeHom.id _ := rfl

/- Provided for rewriting. -/
lemma id_apply (X : FinBddDistLat) (x : X) :
    (ðŸ™ X : X âŸ¶ X) x = x := by simp

@[simp]
lemma hom_comp {X Y Z : FinBddDistLat} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    (f â‰« g).hom = g.hom.comp f.hom := rfl

/- Provided for rewriting. -/
lemma comp_apply {X Y Z : FinBddDistLat} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (x : X) :
    (f â‰« g) x = g (f x) := by simp

@[ext]
lemma hom_ext {X Y : FinBddDistLat} {f g : X âŸ¶ Y} (hf : f.hom = g.hom) : f = g :=
  Hom.ext hf

@[simp]
lemma hom_ofHom {X Y : Type u} [DistribLattice X] [BoundedOrder X] [Fintype X] [DistribLattice Y]
    [BoundedOrder Y] [Fintype Y] (f : BoundedLatticeHom X Y) : (ofHom f).hom = f := rfl

@[simp]
lemma ofHom_hom {X Y : FinBddDistLat} (f : X âŸ¶ Y) :
    ofHom (Hom.hom f) = f := rfl

@[simp]
lemma ofHom_id {X : Type u} [DistribLattice X] [BoundedOrder X] [Fintype X] :
    ofHom (BoundedLatticeHom.id _) = ðŸ™ (of X) := rfl

@[simp]
lemma ofHom_comp {X Y Z : Type u} [DistribLattice X] [BoundedOrder X] [Fintype X] [DistribLattice Y]
    [BoundedOrder Y] [Fintype Y] [DistribLattice Z] [BoundedOrder Z] [Fintype Z]
    (f : BoundedLatticeHom X Y) (g : BoundedLatticeHom Y Z) :
    ofHom (g.comp f) = ofHom f â‰« ofHom g :=
  rfl

lemma ofHom_apply {X Y : Type u} [DistribLattice X] [BoundedOrder X] [Fintype X] [DistribLattice Y]
    [BoundedOrder Y] [Fintype Y]
    (f : BoundedLatticeHom X Y) (x : X) :
    (ofHom f) x = f x := rfl

lemma inv_hom_apply {X Y : FinBddDistLat} (e : X â‰… Y) (x : X) : e.inv (e.hom x) = x := by
  simp

lemma hom_inv_apply {X Y : FinBddDistLat} (e : X â‰… Y) (s : Y) : e.hom (e.inv s) = s := by
  simp

instance : Inhabited FinBddDistLat :=
  âŸ¨of PUnitâŸ©

instance hasForgetToBddDistLat : HasForgetâ‚‚ FinBddDistLat BddDistLat where
  forgetâ‚‚.obj X := .of X
  forgetâ‚‚.map f := BddDistLat.ofHom f.hom

instance hasForgetToFinPartOrd : HasForgetâ‚‚ FinBddDistLat FinPartOrd where
  forgetâ‚‚.obj X := .of X
  forgetâ‚‚.map f := PartOrd.ofHom (OrderHomClass.toOrderHom f.hom)

/-- Constructs an equivalence between finite distributive lattices from an order isomorphism
between them. -/
@[simps]
def Iso.mk {Î± Î² : FinBddDistLat.{u}} (e : Î±.carrier â‰ƒo Î².carrier) : Î± â‰… Î² where
  hom := ofHom e
  inv := ofHom e.symm
  hom_inv_id := by ext; exact e.symm_apply_apply _
  inv_hom_id := by ext; exact e.apply_symm_apply _

/-- `OrderDual` as a functor. -/
@[simps map]
def dual : FinBddDistLat â¥¤ FinBddDistLat where
  obj X := of Xáµ’áµˆ
  map f := ofHom f.hom.dual

/-- The equivalence between `FinBddDistLat` and itself induced by `OrderDual` both ways. -/
@[simps functor inverse]
def dualEquiv : FinBddDistLat â‰Œ FinBddDistLat where
  functor := dual
  inverse := dual
  unitIso := NatIso.ofComponents (fun X => Iso.mk (Î± := X) <| OrderIso.dualDual X)
  counitIso := NatIso.ofComponents (fun X => Iso.mk <| OrderIso.dualDual X)

end FinBddDistLat

theorem finBddDistLat_dual_comp_forget_to_bddDistLat :
    FinBddDistLat.dual â‹™ forgetâ‚‚ FinBddDistLat BddDistLat =
      forgetâ‚‚ FinBddDistLat BddDistLat â‹™ BddDistLat.dual :=
  rfl
