/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Order.Category.Lat

/-!
# The category of distributive lattices

This file defines `DistLat`, the category of distributive lattices.

Note that [`DistLat`](https://ncatlab.org/nlab/show/DistLat) in the literature doesn't always
correspond to `DistLat` as we don't require bottom or top elements. Instead, this `DistLat`
corresponds to `BddDistLat`.
-/


universe u

open CategoryTheory

/-- The category of distributive lattices. -/
structure DistLat where
  /-- The underlying distributive lattice. -/
  carrier : Type*
  [str : DistribLattice carrier]

attribute [instance] DistLat.str

initialize_simps_projections DistLat (carrier â†’ coe, -str)

namespace DistLat

instance : CoeSort DistLat.{u} (Type u) :=
  âŸ¨DistLat.carrierâŸ©

attribute [coe] DistLat.carrier

/-- Construct a bundled `DistLat` from the underlying type and typeclass. -/
abbrev of (X : Type*) [DistribLattice X] : DistLat := âŸ¨XâŸ©

/-- The type of morphisms in `DistLat R`. -/
@[ext]
structure Hom (X Y : DistLat.{u}) where
  private mk ::
  /-- The underlying `LatticeHom`. -/
  hom' : LatticeHom X Y

instance : Category DistLat.{u} where
  Hom X Y := Hom X Y
  id X := âŸ¨LatticeHom.id XâŸ©
  comp f g := âŸ¨g.hom'.comp f.hom'âŸ©

instance : ConcreteCategory DistLat (LatticeHom Â· Â·) where
  hom := Hom.hom'
  ofHom := Hom.mk

/-- Turn a morphism in `DistLat` back into a `LatticeHom`. -/
abbrev Hom.hom {X Y : DistLat.{u}} (f : Hom X Y) :=
  ConcreteCategory.hom (C := DistLat) f

/-- Typecheck a `LatticeHom` as a morphism in `DistLat`. -/
abbrev ofHom {X Y : Type u} [DistribLattice X] [DistribLattice Y] (f : LatticeHom X Y) :
    of X âŸ¶ of Y :=
  ConcreteCategory.ofHom (C := DistLat) f

variable {R} in
/-- Use the `ConcreteCategory.hom` projection for `@[simps]` lemmas. -/
def Hom.Simps.hom (X Y : DistLat.{u}) (f : Hom X Y) :=
  f.hom

initialize_simps_projections Hom (hom' â†’ hom)

/-!
The results below duplicate the `ConcreteCategory` simp lemmas, but we can keep them for `dsimp`.
-/

@[simp]
lemma coe_id {X : DistLat} : (ðŸ™ X : X â†’ X) = id := rfl

@[simp]
lemma coe_comp {X Y Z : DistLat} {f : X âŸ¶ Y} {g : Y âŸ¶ Z} : (f â‰« g : X â†’ Z) = g âˆ˜ f := rfl

@[simp]
lemma forget_map {X Y : DistLat} (f : X âŸ¶ Y) :
    (forget DistLat).map f = f := rfl

@[ext]
lemma ext {X Y : DistLat} {f g : X âŸ¶ Y} (w : âˆ€ x : X, f x = g x) : f = g :=
  ConcreteCategory.hom_ext _ _ w

-- This is not `simp` to avoid rewriting in types of terms.
theorem coe_of (X : Type u) [DistribLattice X] : (DistLat.of X : Type u) = X := rfl

@[simp]
lemma hom_id {X : DistLat} : (ðŸ™ X : X âŸ¶ X).hom = LatticeHom.id _ := rfl

/- Provided for rewriting. -/
lemma id_apply (X : DistLat) (x : X) :
    (ðŸ™ X : X âŸ¶ X) x = x := by simp

@[simp]
lemma hom_comp {X Y Z : DistLat} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    (f â‰« g).hom = g.hom.comp f.hom := rfl

/- Provided for rewriting. -/
lemma comp_apply {X Y Z : DistLat} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (x : X) :
    (f â‰« g) x = g (f x) := by simp

@[ext]
lemma hom_ext {X Y : DistLat} {f g : X âŸ¶ Y} (hf : f.hom = g.hom) : f = g :=
  Hom.ext hf

@[simp]
lemma hom_ofHom {X Y : Type u} [DistribLattice X] [DistribLattice Y] (f : LatticeHom X Y) :
    (ofHom f).hom = f :=
  rfl

@[simp]
lemma ofHom_hom {X Y : DistLat} (f : X âŸ¶ Y) :
    ofHom (Hom.hom f) = f := rfl

@[simp]
lemma ofHom_id {X : Type u} [DistribLattice X] : ofHom (LatticeHom.id _) = ðŸ™ (of X) := rfl

@[simp]
lemma ofHom_comp {X Y Z : Type u} [DistribLattice X] [DistribLattice Y] [DistribLattice Z]
    (f : LatticeHom X Y) (g : LatticeHom Y Z) :
    ofHom (g.comp f) = ofHom f â‰« ofHom g :=
  rfl

lemma ofHom_apply {X Y : Type u} [DistribLattice X] [DistribLattice Y]
    (f : LatticeHom X Y) (x : X) :
    (ofHom f) x = f x := rfl

lemma inv_hom_apply {X Y : DistLat} (e : X â‰… Y) (x : X) : e.inv (e.hom x) = x := by
  simp

lemma hom_inv_apply {X Y : DistLat} (e : X â‰… Y) (s : Y) : e.hom (e.inv s) = s := by
  simp

instance hasForgetToLat : HasForgetâ‚‚ DistLat Lat where
  forgetâ‚‚.obj X := .of X
  forgetâ‚‚.map f := Lat.ofHom f.hom

/-- Constructs an equivalence between distributive lattices from an order isomorphism between them.
-/
@[simps]
def Iso.mk {Î± Î² : DistLat.{u}} (e : Î± â‰ƒo Î²) : Î± â‰… Î² where
  hom := ofHom e
  inv := ofHom e.symm

/-- `OrderDual` as a functor. -/
@[simps map]
def dual : DistLat â¥¤ DistLat where
  obj X := of Xáµ’áµˆ
  map f := ofHom f.hom.dual

/-- The equivalence between `DistLat` and itself induced by `OrderDual` both ways. -/
@[simps functor inverse]
def dualEquiv : DistLat â‰Œ DistLat where
  functor := dual
  inverse := dual
  unitIso := NatIso.ofComponents (fun X => Iso.mk <| OrderIso.dualDual X) fun _ => rfl
  counitIso := NatIso.ofComponents (fun X => Iso.mk <| OrderIso.dualDual X) fun _ => rfl

end DistLat

theorem distLat_dual_comp_forget_to_Lat :
    DistLat.dual â‹™ forgetâ‚‚ DistLat Lat = forgetâ‚‚ DistLat Lat â‹™ Lat.dual :=
  rfl
