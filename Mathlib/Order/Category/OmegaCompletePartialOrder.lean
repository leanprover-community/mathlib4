/-
Copyright (c) 2020 Simon Hudon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Simon Hudon
-/
import Mathlib.Order.OmegaCompletePartialOrder
import Mathlib.CategoryTheory.Limits.Shapes.Products
import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
import Mathlib.CategoryTheory.Limits.Constructions.LimitsOfProductsAndEqualizers
import Mathlib.CategoryTheory.ConcreteCategory.Basic

/-!
# Category of types with an omega complete partial order

In this file, we bundle the class `OmegaCompletePartialOrder` into a
concrete category and prove that continuous functions also form
an `OmegaCompletePartialOrder`.

## Main definitions

* `Ï‰CPO`
  * an instance of `Category` and `ConcreteCategory`

-/


open CategoryTheory

universe u v


/-- The category of types with an omega complete partial order. -/
structure Ï‰CPO : Type (u + 1) where
  /-- The underlying type. -/
  carrier : Type u
  [str : OmegaCompletePartialOrder carrier]

attribute [instance] Ï‰CPO.str

namespace Ï‰CPO

open OmegaCompletePartialOrder

instance : CoeSort Ï‰CPO Type* :=
  âŸ¨carrierâŸ©

/-- Construct a bundled Ï‰CPO from the underlying type and typeclass. -/
abbrev of (Î± : Type*) [OmegaCompletePartialOrder Î±] : Ï‰CPO where
  carrier := Î±

theorem coe_of (Î± : Type*) [OmegaCompletePartialOrder Î±] : â†¥(of Î±) = Î± :=
  rfl

instance : LargeCategory.{u} Ï‰CPO where
  Hom X Y := ContinuousHom X Y
  id X := ContinuousHom.id
  comp f g := g.comp f

instance : ConcreteCategory Ï‰CPO (ContinuousHom Â· Â·) where
  hom f := f
  ofHom f := f

instance : Inhabited Ï‰CPO :=
  âŸ¨of PUnitâŸ©

section

open CategoryTheory.Limits

namespace HasProducts

/-- The pi-type gives a cone for a product. -/
def product {J : Type v} (f : J â†’ Ï‰CPO.{v}) : Fan f :=
  Fan.mk (of (âˆ€ j, f j)) fun j => .mk (Pi.evalOrderHom j) fun _ => rfl

/-- The pi-type is a limit cone for the product. -/
def isProduct (J : Type v) (f : J â†’ Ï‰CPO) : IsLimit (product f) where
  lift s :=
    âŸ¨âŸ¨fun t j => (s.Ï€.app âŸ¨jâŸ©) t, fun _ _ h j => (s.Ï€.app âŸ¨jâŸ©).monotone hâŸ©,
      fun x => funext fun j => (s.Ï€.app âŸ¨jâŸ©).continuous xâŸ©
  uniq s m w := by
    ext t; funext j -- Porting note (https://github.com/leanprover-community/mathlib4/issues/11041): Originally `ext t j`
    change m t j = (s.Ï€.app âŸ¨jâŸ©) t
    rw [â† w âŸ¨jâŸ©]
    rfl
  fac _ _ := rfl

instance (J : Type v) (f : J â†’ Ï‰CPO.{v}) : HasProduct f :=
  HasLimit.mk âŸ¨_, isProduct _ fâŸ©

end HasProducts

instance omegaCompletePartialOrderEqualizer {Î± Î² : Type*} [OmegaCompletePartialOrder Î±]
    [OmegaCompletePartialOrder Î²] (f g : Î± â†’ð’„ Î²) :
    OmegaCompletePartialOrder { a : Î± // f a = g a } :=
  OmegaCompletePartialOrder.subtype _ fun c hc => by
    rw [f.continuous, g.continuous]
    congr 1
    apply OrderHom.ext; funext x -- Porting note (https://github.com/leanprover-community/mathlib4/issues/11041): Originally `ext`
    apply hc _ âŸ¨_, rflâŸ©

namespace HasEqualizers

/-- The equalizer inclusion function as a `ContinuousHom`. -/
def equalizerÎ¹ {Î± Î² : Type*} [OmegaCompletePartialOrder Î±] [OmegaCompletePartialOrder Î²]
    (f g : Î± â†’ð’„ Î²) : { a : Î± // f a = g a } â†’ð’„ Î± :=
  .mk (OrderHom.Subtype.val _) fun _ => rfl

/-- A construction of the equalizer fork. -/
def equalizer {X Y : Ï‰CPO.{v}} (f g : X âŸ¶ Y) : Fork f g :=
  Fork.ofÎ¹ (P := Ï‰CPO.of { a // f a = g a }) (equalizerÎ¹ f g)
    (ContinuousHom.ext _ _ fun x => x.2)

/-- The equalizer fork is a limit. -/
def isEqualizer {X Y : Ï‰CPO.{v}} (f g : X âŸ¶ Y) : IsLimit (equalizer f g) :=
  Fork.IsLimit.mk' _ fun s =>
    âŸ¨{  toFun := fun x => âŸ¨s.Î¹ x, by apply ContinuousHom.congr_fun s.conditionâŸ©
        monotone' := fun _ _ h => s.Î¹.monotone h
        map_Ï‰Sup' := fun x => Subtype.ext (s.Î¹.continuous x)
      }, by ext; rfl, fun hm => by
      ext x : 2; apply Subtype.ext ?_ -- Porting note (https://github.com/leanprover-community/mathlib4/issues/11041): Originally `ext`
      apply ContinuousHom.congr_fun hmâŸ©

end HasEqualizers

instance : HasProducts.{v} Ï‰CPO.{v} :=
  fun _ => { has_limit := fun _ => hasLimit_of_iso Discrete.natIsoFunctor.symm }

instance {X Y : Ï‰CPO.{v}} (f g : X âŸ¶ Y) : HasLimit (parallelPair f g) :=
  HasLimit.mk âŸ¨_, HasEqualizers.isEqualizer f gâŸ©

instance : HasEqualizers Ï‰CPO.{v} :=
  hasEqualizers_of_hasLimit_parallelPair _

instance : HasLimits Ï‰CPO.{v} :=
  has_limits_of_hasEqualizers_and_products

end

end Ï‰CPO
