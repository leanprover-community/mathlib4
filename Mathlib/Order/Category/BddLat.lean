/-
Copyright (c) 2022 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies
-/
import Mathlib.CategoryTheory.Adjunction.Unique
import Mathlib.Order.Category.BddOrd
import Mathlib.Order.Category.Lat
import Mathlib.Order.Category.Semilat
import Mathlib.Order.Hom.WithTopBot

/-!
# The category of bounded lattices

This file defines `BddLat`, the category of bounded lattices.

In literature, this is sometimes called `Lat`, the category of lattices, because being a lattice is
understood to entail having a bottom and a top element.
-/


universe u

open CategoryTheory

/-- The category of bounded lattices with bounded lattice morphisms. -/
structure BddLat extends Lat where
  [isBoundedOrder : BoundedOrder toLat]

/-- The underlying lattice of a bounded lattice. -/
add_decl_doc BddLat.toLat

namespace BddLat

instance : CoeSort BddLat Type* :=
  ‚ü®fun X ‚Ü¶ X.toLat‚ü©

attribute [instance] BddLat.isBoundedOrder

/-- Construct a bundled `BddLat` from `Lattice` + `BoundedOrder`. -/
abbrev of (Œ± : Type*) [Lattice Œ±] [BoundedOrder Œ±] : BddLat where
  carrier := Œ±

theorem coe_of (Œ± : Type*) [Lattice Œ±] [BoundedOrder Œ±] : ‚Ü•(of Œ±) = Œ± :=
  rfl

/-- The type of morphisms in `BddLat`. -/
@[ext]
structure Hom (X Y : BddLat.{u}) where
  private mk ::
  /-- The underlying `BoundedLatticeHom`. -/
  hom' : BoundedLatticeHom X Y

instance : Inhabited BddLat :=
  ‚ü®of PUnit‚ü©

instance : LargeCategory.{u} BddLat where
  Hom := Hom
  id X := ‚ü®BoundedLatticeHom.id X‚ü©
  comp f g := ‚ü®g.hom'.comp f.hom'‚ü©

instance : ConcreteCategory BddLat (BoundedLatticeHom ¬∑ ¬∑) where
  hom := Hom.hom'
  ofHom := Hom.mk

/-- Turn a morphism in `BddLat` back into a `BoundedLatticeHom`. -/
abbrev Hom.hom {X Y : BddLat.{u}} (f : Hom X Y) :=
  ConcreteCategory.hom (C := BddLat) f

/-- Typecheck a `BoundedLatticeHom` as a morphism in `BddLat`. -/
abbrev ofHom {X Y : Type u} [Lattice X] [BoundedOrder X] [Lattice Y] [BoundedOrder Y]
    (f : BoundedLatticeHom X Y) : of X ‚ü∂ of Y :=
  ConcreteCategory.ofHom (C := BddLat) f

variable {R} in
/-- Use the `ConcreteCategory.hom` projection for `@[simps]` lemmas. -/
def Hom.Simps.hom (X Y : BddLat.{u}) (f : Hom X Y) :=
  f.hom

initialize_simps_projections Hom (hom' ‚Üí hom)

@[simp]
lemma hom_id {X : Lat} : (ùüô X : X ‚ü∂ X).hom = LatticeHom.id _ := rfl

/- Provided for rewriting. -/
lemma id_apply (X : Lat) (x : X) :
    (ùüô X : X ‚ü∂ X) x = x := by simp

@[simp]
lemma hom_comp {X Y Z : Lat} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).hom = g.hom.comp f.hom := rfl

/- Provided for rewriting. -/
lemma comp_apply {X Y Z : Lat} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) :
    (f ‚â´ g) x = g (f x) := by simp

@[ext]
lemma ext {X Y : BddLat} {f g : X ‚ü∂ Y} (w : ‚àÄ x : X, f x = g x) : f = g :=
  ConcreteCategory.hom_ext _ _ w

@[ext]
lemma hom_ext {X Y : BddLat} {f g : X ‚ü∂ Y} (hf : f.hom = g.hom) : f = g :=
  Hom.ext hf

instance hasForgetToBddOrd : HasForget‚ÇÇ BddLat BddOrd where
  forget‚ÇÇ.obj X := .of X
  forget‚ÇÇ.map f := BddOrd.ofHom f.hom.toBoundedOrderHom

instance hasForgetToLat : HasForget‚ÇÇ BddLat Lat where
  forget‚ÇÇ.obj X := .of X
  forget‚ÇÇ.map f := Lat.ofHom f.hom.toLatticeHom

instance hasForgetToSemilatSup : HasForget‚ÇÇ BddLat SemilatSupCat where
  forget‚ÇÇ.obj X := .of X
  forget‚ÇÇ.map f := f.hom.toSupBotHom

instance hasForgetToSemilatInf : HasForget‚ÇÇ BddLat SemilatInfCat where
  forget‚ÇÇ.obj X := .of X
  forget‚ÇÇ.map f := f.hom.toInfTopHom

@[simp]
theorem coe_forget_to_bddOrd (X : BddLat) : ‚Ü•((forget‚ÇÇ BddLat BddOrd).obj X) = ‚Ü•X :=
  rfl

@[simp]
theorem coe_forget_to_lat (X : BddLat) : ‚Ü•((forget‚ÇÇ BddLat Lat).obj X) = ‚Ü•X :=
  rfl

@[simp]
theorem coe_forget_to_semilatSup (X : BddLat) :
    ‚Ü•((forget‚ÇÇ BddLat SemilatSupCat).obj X) = ‚Ü•X :=
  rfl

@[simp]
theorem coe_forget_to_semilatInf (X : BddLat) :
    ‚Ü•((forget‚ÇÇ BddLat SemilatInfCat).obj X) = ‚Ü•X :=
  rfl

theorem forget_lat_partOrd_eq_forget_bddOrd_partOrd :
    forget‚ÇÇ BddLat Lat ‚ãô forget‚ÇÇ Lat PartOrd =
      forget‚ÇÇ BddLat BddOrd ‚ãô forget‚ÇÇ BddOrd PartOrd :=
  rfl

theorem forget_semilatSup_partOrd_eq_forget_bddOrd_partOrd :
    forget‚ÇÇ BddLat SemilatSupCat ‚ãô forget‚ÇÇ SemilatSupCat PartOrd =
      forget‚ÇÇ BddLat BddOrd ‚ãô forget‚ÇÇ BddOrd PartOrd :=
  rfl

theorem forget_semilatInf_partOrd_eq_forget_bddOrd_partOrd :
    forget‚ÇÇ BddLat SemilatInfCat ‚ãô forget‚ÇÇ SemilatInfCat PartOrd =
      forget‚ÇÇ BddLat BddOrd ‚ãô forget‚ÇÇ BddOrd PartOrd :=
  rfl

/-- Constructs an equivalence between bounded lattices from an order isomorphism
between them. -/
@[simps]
def Iso.mk {Œ± Œ≤ : BddLat.{u}} (e : Œ± ‚âÉo Œ≤) : Œ± ‚âÖ Œ≤ where
  hom := ofHom e
  inv := ofHom e.symm
  hom_inv_id := by ext; exact e.symm_apply_apply _
  inv_hom_id := by ext; exact e.apply_symm_apply _

/-- `OrderDual` as a functor. -/
@[simps map]
def dual : BddLat ‚•§ BddLat where
  obj X := of X·µí·µà
  map f := ofHom f.hom.dual

/-- The equivalence between `BddLat` and itself induced by `OrderDual` both ways. -/
@[simps functor inverse]
def dualEquiv : BddLat ‚âå BddLat where
  functor := dual
  inverse := dual
  unitIso := NatIso.ofComponents fun X ‚Ü¶ Iso.mk <| OrderIso.dualDual X
  counitIso := NatIso.ofComponents fun X ‚Ü¶ Iso.mk <| OrderIso.dualDual X

end BddLat

theorem bddLat_dual_comp_forget_to_bddOrd :
    BddLat.dual ‚ãô forget‚ÇÇ BddLat BddOrd =
    forget‚ÇÇ BddLat BddOrd ‚ãô BddOrd.dual :=
  rfl

theorem bddLat_dual_comp_forget_to_lat :
    BddLat.dual ‚ãô forget‚ÇÇ BddLat Lat = forget‚ÇÇ BddLat Lat ‚ãô Lat.dual :=
  rfl

theorem bddLat_dual_comp_forget_to_semilatSupCat :
    BddLat.dual ‚ãô forget‚ÇÇ BddLat SemilatSupCat =
    forget‚ÇÇ BddLat SemilatInfCat ‚ãô SemilatInfCat.dual :=
  rfl

theorem bddLat_dual_comp_forget_to_semilatInfCat :
    BddLat.dual ‚ãô forget‚ÇÇ BddLat SemilatInfCat =
    forget‚ÇÇ BddLat SemilatSupCat ‚ãô SemilatSupCat.dual :=
  rfl

/-- The functor that adds a bottom and a top element to a lattice. This is the free functor. -/
def latToBddLat : Lat.{u} ‚•§ BddLat where
  obj X := .of <| WithTop <| WithBot X
  map f := BddLat.ofHom <| LatticeHom.withTopWithBot f.hom

/-- `latToBddLat` is left adjoint to the forgetful functor, meaning it is the free
functor from `Lat` to `BddLat`. -/
def latToBddLatForgetAdjunction : latToBddLat.{u} ‚ä£ forget‚ÇÇ BddLat Lat :=
  Adjunction.mkOfHomEquiv
    { homEquiv X _ :=
        { toFun f := Lat.ofHom
            { toFun := f ‚àò some ‚àò some
              map_sup' := fun a b ‚Ü¶ (congr_arg f <| by rfl).trans (f.hom.map_sup' _ _)
              map_inf' := fun a b ‚Ü¶ (congr_arg f <| by rfl).trans (f.hom.map_inf' _ _) }
          invFun f := BddLat.ofHom <| LatticeHom.withTopWithBot' f.hom
          left_inv := fun f ‚Ü¶
            BddLat.ext fun a ‚Ü¶
              match a with
              | none => f.hom.map_top'.symm
              | some none => f.hom.map_bot'.symm
              | some (some _) => rfl }
      homEquiv_naturality_left_symm := fun _ _ ‚Ü¶
        BddLat.ext fun a ‚Ü¶
          match a with
          | none => rfl
          | some none => rfl
          | some (some _) => rfl
      homEquiv_naturality_right := fun _ _ ‚Ü¶ Lat.ext fun _ ‚Ü¶ rfl }

/-- `latToBddLat` and `OrderDual` commute. -/
def latToBddLatCompDualIsoDualCompLatToBddLat :
    latToBddLat.{u} ‚ãô BddLat.dual ‚âÖ Lat.dual ‚ãô latToBddLat :=
  Adjunction.leftAdjointUniq (latToBddLatForgetAdjunction.comp BddLat.dualEquiv.toAdjunction)
    (Lat.dualEquiv.toAdjunction.comp latToBddLatForgetAdjunction)
