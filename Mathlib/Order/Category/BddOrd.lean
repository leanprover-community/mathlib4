/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.CategoryTheory.Category.Bipointed
import Mathlib.Order.Category.PartOrd
import Mathlib.Order.Hom.Bounded

/-!
# The category of bounded orders

This defines `BddOrd`, the category of bounded orders.
-/


universe u v

open CategoryTheory

/-- The category of bounded orders with monotone functions. -/
structure BddOrd extends PartOrd where
  [isBoundedOrder : BoundedOrder toPartOrd]

/-- The underlying object in the category of partial orders. -/
add_decl_doc BddOrd.toPartOrd

attribute [instance] BddOrd.isBoundedOrder

initialize_simps_projections BddOrd (carrier â†’ coe, -str)

namespace BddOrd

instance : CoeSort BddOrd Type* :=
  InducedCategory.hasCoeToSort toPartOrd

/-- Construct a bundled `BddOrd` from the underlying type and typeclass. -/
abbrev of (X : Type*) [PartialOrder X] [BoundedOrder X] : BddOrd where
  carrier := X

/-- The type of morphisms in `BddOrd R`. -/
@[ext]
structure Hom (X Y : BddOrd.{u}) where
  private mk ::
  /-- The underlying `BoundedOrderHom`. -/
  hom' : BoundedOrderHom X Y

instance : Category BddOrd.{u} where
  Hom X Y := Hom X Y
  id _ := âŸ¨BoundedOrderHom.id _âŸ©
  comp f g := âŸ¨g.hom'.comp f.hom'âŸ©

instance : ConcreteCategory BddOrd (BoundedOrderHom Â· Â·) where
  hom := Hom.hom'
  ofHom := Hom.mk

/-- Turn a morphism in `BddOrd` back into a `BoundedOrderHom`. -/
abbrev Hom.hom {X Y : BddOrd.{u}} (f : Hom X Y) :=
  ConcreteCategory.hom (C := BddOrd) f

/-- Typecheck a `BoundedOrderHom` as a morphism in `BddOrd`. -/
abbrev ofHom {X Y : Type u} [PartialOrder X] [BoundedOrder X] [PartialOrder Y] [BoundedOrder Y]
    (f : BoundedOrderHom X Y) : of X âŸ¶ of Y :=
  ConcreteCategory.ofHom (C := BddOrd) f

variable {R} in
/-- Use the `ConcreteCategory.hom` projection for `@[simps]` lemmas. -/
def Hom.Simps.hom (X Y : BddOrd.{u}) (f : Hom X Y) :=
  f.hom

initialize_simps_projections Hom (hom' â†’ hom)

/-!
The results below duplicate the `ConcreteCategory` simp lemmas, but we can keep them for `dsimp`.
-/

@[simp]
lemma coe_id {X : BddOrd} : (ðŸ™ X : X â†’ X) = id := rfl

@[simp]
lemma coe_comp {X Y Z : BddOrd} {f : X âŸ¶ Y} {g : Y âŸ¶ Z} : (f â‰« g : X â†’ Z) = g âˆ˜ f := rfl

@[simp]
lemma forget_map {X Y : BddOrd} (f : X âŸ¶ Y) :
    (forget BddOrd).map f = f := rfl

@[ext]
lemma ext {X Y : BddOrd} {f g : X âŸ¶ Y} (w : âˆ€ x : X, f x = g x) : f = g :=
  ConcreteCategory.hom_ext _ _ w

-- This is not `simp` to avoid rewriting in types of terms.
theorem coe_of (X : Type u) [PartialOrder X] [BoundedOrder X] : (BddOrd.of X : Type u) = X := rfl

@[simp]
lemma hom_id {X : BddOrd} : (ðŸ™ X : X âŸ¶ X).hom = BoundedOrderHom.id _ := rfl

/- Provided for rewriting. -/
lemma id_apply (X : BddOrd) (x : X) :
    (ðŸ™ X : X âŸ¶ X) x = x := by simp

@[simp]
lemma hom_comp {X Y Z : BddOrd} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    (f â‰« g).hom = g.hom.comp f.hom := rfl

/- Provided for rewriting. -/
lemma comp_apply {X Y Z : BddOrd} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (x : X) :
    (f â‰« g) x = g (f x) := by simp

@[ext]
lemma hom_ext {X Y : BddOrd} {f g : X âŸ¶ Y} (hf : f.hom = g.hom) : f = g :=
  Hom.ext hf

@[simp]
lemma hom_ofHom {X Y : Type u} [PartialOrder X] [BoundedOrder X] [PartialOrder Y] [BoundedOrder Y]
    (f : BoundedOrderHom X Y) :
  (ofHom f).hom = f := rfl

@[simp]
lemma ofHom_hom {X Y : BddOrd} (f : X âŸ¶ Y) :
    ofHom f.hom = f := rfl

@[simp]
lemma ofHom_id {X : Type u} [PartialOrder X] [BoundedOrder X] :
    ofHom (BoundedOrderHom.id _) = ðŸ™ (of X) := rfl

@[simp]
lemma ofHom_comp {X Y Z : Type u} [PartialOrder X] [BoundedOrder X] [PartialOrder Y]
    [BoundedOrder Y] [PartialOrder Z] [BoundedOrder Z]
    (f : BoundedOrderHom X Y) (g : BoundedOrderHom Y Z) :
    ofHom (g.comp f) = ofHom f â‰« ofHom g :=
  rfl

lemma ofHom_apply {X Y : Type u} [PartialOrder X] [BoundedOrder X] [PartialOrder Y] [BoundedOrder Y]
    (f : BoundedOrderHom X Y) (x : X) :
    ofHom f x = f x := rfl

lemma inv_hom_apply {X Y : BddOrd} (e : X â‰… Y) (x : X) : e.inv (e.hom x) = x := by
  simp

lemma hom_inv_apply {X Y : BddOrd} (e : X â‰… Y) (s : Y) : e.hom (e.inv s) = s := by
  simp

instance : Inhabited BddOrd :=
  âŸ¨of PUnitâŸ©

instance hasForgetToPartOrd : HasForgetâ‚‚ BddOrd PartOrd where
  forgetâ‚‚.obj X := X.toPartOrd
  forgetâ‚‚.map f := PartOrd.ofHom f.hom.toOrderHom

instance hasForgetToBipointed : HasForgetâ‚‚ BddOrd Bipointed where
  forgetâ‚‚ :=
    { obj := fun X => âŸ¨X, âŠ¥, âŠ¤âŸ©
      map := fun f => âŸ¨f, f.hom.map_bot', f.hom.map_top'âŸ© }
  forget_comp := rfl

/-- `OrderDual` as a functor. -/
@[simps map]
def dual : BddOrd â¥¤ BddOrd where
  obj X := of Xáµ’áµˆ
  map f := ofHom f.hom.dual

/-- Constructs an equivalence between bounded orders from an order isomorphism between them. -/
@[simps]
def Iso.mk {Î± Î² : BddOrd.{u}} (e : Î± â‰ƒo Î²) : Î± â‰… Î² where
  hom := ofHom e
  inv := ofHom e.symm
  hom_inv_id := by ext; exact e.symm_apply_apply _
  inv_hom_id := by ext; exact e.apply_symm_apply _

/-- The equivalence between `BddOrd` and itself induced by `OrderDual` both ways. -/
@[simps functor inverse]
def dualEquiv : BddOrd â‰Œ BddOrd where
  functor := dual
  inverse := dual
  unitIso := NatIso.ofComponents fun X => Iso.mk <| OrderIso.dualDual X
  counitIso := NatIso.ofComponents fun X => Iso.mk <| OrderIso.dualDual X

end BddOrd

theorem bddOrd_dual_comp_forget_to_partOrd :
    BddOrd.dual â‹™ forgetâ‚‚ BddOrd PartOrd =
    forgetâ‚‚ BddOrd PartOrd â‹™ PartOrd.dual :=
  rfl

theorem bddOrd_dual_comp_forget_to_bipointed :
    BddOrd.dual â‹™ forgetâ‚‚ BddOrd Bipointed =
    forgetâ‚‚ BddOrd Bipointed â‹™ Bipointed.swap :=
  rfl
