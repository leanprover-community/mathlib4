/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/
import Mathlib.Order.Antisymmetrization
import Mathlib.Order.Category.Preord
import Mathlib.CategoryTheory.Adjunction.Basic

/-!
# Category of partial orders

This defines `PartOrd`, the category of partial orders with monotone maps.
-/

open CategoryTheory

universe u

/-- The category of partial orders. -/
structure PartOrd where
  /-- The underlying partially ordered type. -/
  (carrier : Type*)
  [str : PartialOrder carrier]

attribute [instance] PartOrd.str

initialize_simps_projections PartOrd (carrier ‚Üí coe, -str)

namespace PartOrd

instance : CoeSort PartOrd (Type _) :=
  ‚ü®PartOrd.carrier‚ü©

attribute [coe] PartOrd.carrier

/-- Construct a bundled `PartOrd` from the underlying type and typeclass. -/
abbrev of (X : Type*) [PartialOrder X] : PartOrd := ‚ü®X‚ü©

/-- The type of morphisms in `PartOrd R`. -/
@[ext]
structure Hom (X Y : PartOrd.{u}) where
  private mk ::
  /-- The underlying `OrderHom`. -/
  hom' : X ‚Üío Y

instance : Category PartOrd.{u} where
  Hom X Y := Hom X Y
  id _ := ‚ü®OrderHom.id‚ü©
  comp f g := ‚ü®g.hom'.comp f.hom'‚ü©

instance : ConcreteCategory PartOrd (¬∑ ‚Üío ¬∑) where
  hom := Hom.hom'
  ofHom := Hom.mk

/-- Turn a morphism in `PartOrd` back into a `OrderHom`. -/
abbrev Hom.hom {X Y : PartOrd.{u}} (f : Hom X Y) :=
  ConcreteCategory.hom (C := PartOrd) f

/-- Typecheck a `OrderHom` as a morphism in `PartOrd`. -/
abbrev ofHom {X Y : Type u} [PartialOrder X] [PartialOrder Y] (f : X ‚Üío Y) : of X ‚ü∂ of Y :=
  ConcreteCategory.ofHom (C := PartOrd) f

variable {R} in
/-- Use the `ConcreteCategory.hom` projection for `@[simps]` lemmas. -/
def Hom.Simps.hom (X Y : PartOrd.{u}) (f : Hom X Y) :=
  f.hom

initialize_simps_projections Hom (hom' ‚Üí hom)

/-!
The results below duplicate the `ConcreteCategory` simp lemmas, but we can keep them for `dsimp`.
-/

@[simp]
lemma coe_id {X : PartOrd} : (ùüô X : X ‚Üí X) = id := rfl

@[simp]
lemma coe_comp {X Y Z : PartOrd} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z} : (f ‚â´ g : X ‚Üí Z) = g ‚àò f := rfl

@[simp]
lemma forget_map {X Y : PartOrd} (f : X ‚ü∂ Y) :
    (forget PartOrd).map f = f := rfl

@[ext]
lemma ext {X Y : PartOrd} {f g : X ‚ü∂ Y} (w : ‚àÄ x : X, f x = g x) : f = g :=
  ConcreteCategory.hom_ext _ _ w

-- This is not `simp` to avoid rewriting in types of terms.
theorem coe_of (X : Type u) [PartialOrder X] : (PartOrd.of X : Type u) = X := rfl

@[simp]
lemma hom_id {X : PartOrd} : (ùüô X : X ‚ü∂ X).hom = OrderHom.id := rfl

/- Provided for rewriting. -/
lemma id_apply (X : PartOrd) (x : X) :
    (ùüô X : X ‚ü∂ X) x = x := by simp

@[simp]
lemma hom_comp {X Y Z : PartOrd} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).hom = g.hom.comp f.hom := rfl

/- Provided for rewriting. -/
lemma comp_apply {X Y Z : PartOrd} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) :
    (f ‚â´ g) x = g (f x) := by simp

@[ext]
lemma hom_ext {X Y : PartOrd} {f g : X ‚ü∂ Y} (hf : f.hom = g.hom) : f = g :=
  Hom.ext hf

@[simp]
lemma hom_ofHom {X Y : Type u} [PartialOrder X] [PartialOrder Y] (f : X ‚Üío Y) : (ofHom f).hom = f :=
  rfl

@[simp]
lemma ofHom_hom {X Y : PartOrd} (f : X ‚ü∂ Y) : ofHom (Hom.hom f) = f := rfl

@[simp]
lemma ofHom_id {X : Type u} [PartialOrder X] : ofHom OrderHom.id = ùüô (of X) := rfl

@[simp]
lemma ofHom_comp {X Y Z : Type u} [PartialOrder X] [PartialOrder Y] [PartialOrder Z]
    (f : X ‚Üío Y) (g : Y ‚Üío Z) :
    ofHom (g.comp f) = ofHom f ‚â´ ofHom g :=
  rfl

lemma ofHom_apply {X Y : Type u} [PartialOrder X] [PartialOrder Y] (f : X ‚Üío Y) (x : X) :
    (ofHom f) x = f x := rfl

lemma inv_hom_apply {X Y : PartOrd} (e : X ‚âÖ Y) (x : X) : e.inv (e.hom x) = x := by
  simp

lemma hom_inv_apply {X Y : PartOrd} (e : X ‚âÖ Y) (s : Y) : e.hom (e.inv s) = s := by
  simp

instance hasForgetToPreord : HasForget‚ÇÇ PartOrd Preord where
  forget‚ÇÇ.obj X := .of X
  forget‚ÇÇ.map f := Preord.ofHom f.hom

/-- Constructs an equivalence between partial orders from an order isomorphism between them. -/
@[simps]
def Iso.mk {Œ± Œ≤ : PartOrd.{u}} (e : Œ± ‚âÉo Œ≤) : Œ± ‚âÖ Œ≤ where
  hom := ofHom e
  inv := ofHom e.symm

/-- `OrderDual` as a functor. -/
@[simps map]
def dual : PartOrd ‚•§ PartOrd where
  obj X := of X·µí·µà
  map f := ofHom f.hom.dual

/-- The equivalence between `PartOrd` and itself induced by `OrderDual` both ways. -/
@[simps functor inverse]
def dualEquiv : PartOrd ‚âå PartOrd where
  functor := dual
  inverse := dual
  unitIso := NatIso.ofComponents fun X ‚Ü¶ Iso.mk <| OrderIso.dualDual X
  counitIso := NatIso.ofComponents fun X ‚Ü¶ Iso.mk <| OrderIso.dualDual X

end PartOrd

theorem partOrd_dual_comp_forget_to_preord :
    PartOrd.dual ‚ãô forget‚ÇÇ PartOrd Preord =
      forget‚ÇÇ PartOrd Preord ‚ãô Preord.dual :=
  rfl

/-- `Antisymmetrization` as a functor. It is the free functor. -/
def preordToPartOrd : Preord.{u} ‚•§ PartOrd where
  obj X := .of (Antisymmetrization X (¬∑ ‚â§ ¬∑))
  map f := PartOrd.ofHom f.hom.antisymmetrization
  map_id X := by
    ext x
    exact Quotient.inductionOn' x fun x ‚Ü¶ Quotient.map'_mk'' _ (fun a b ‚Ü¶ id) _
  map_comp f g := by
    ext x
    exact Quotient.inductionOn' x fun x ‚Ü¶ OrderHom.antisymmetrization_apply_mk _ _

/-- `preordToPartOrd` is left adjoint to the forgetful functor, meaning it is the free
functor from `Preord` to `PartOrd`. -/
def preordToPartOrdForgetAdjunction :
    preordToPartOrd.{u} ‚ä£ forget‚ÇÇ PartOrd Preord :=
  Adjunction.mkOfHomEquiv
    { homEquiv _ _ :=
        { toFun f := Preord.ofHom
            ‚ü®f ‚àò toAntisymmetrization (¬∑ ‚â§ ¬∑), f.hom.mono.comp toAntisymmetrization_mono‚ü©
          invFun f := PartOrd.ofHom
            ‚ü®fun a ‚Ü¶ Quotient.liftOn' a f (fun _ _ h ‚Ü¶ (AntisymmRel.image h f.hom.mono).eq),
              fun a b ‚Ü¶ Quotient.inductionOn‚ÇÇ' a b fun _ _ h ‚Ü¶ f.hom.mono h‚ü©
          left_inv _ := PartOrd.ext fun x ‚Ü¶ Quotient.inductionOn' x fun _ ‚Ü¶ rfl }
      homEquiv_naturality_left_symm _ _ :=
        PartOrd.ext fun x ‚Ü¶ Quotient.inductionOn' x fun _ ‚Ü¶ rfl }

-- The `simpNF` linter would complain as `Functor.comp_obj`, `Preord.dual_obj` both apply to LHS
-- of `preordToPartOrdCompToDualIsoToDualCompPreordToPartOrd_hom_app_coe`
/-- `PreordToPartOrd` and `OrderDual` commute. -/
@[simps! -isSimp hom_app_hom_coe inv_app_hom_coe]
def preordToPartOrdCompToDualIsoToDualCompPreordToPartOrd :
    preordToPartOrd.{u} ‚ãô PartOrd.dual ‚âÖ Preord.dual ‚ãô preordToPartOrd :=
  NatIso.ofComponents (fun _ ‚Ü¶ PartOrd.Iso.mk <| OrderIso.dualAntisymmetrization _)
    (fun _ ‚Ü¶ PartOrd.ext fun x ‚Ü¶ Quotient.inductionOn' x fun _ ‚Ü¶ rfl)

-- `simp`-normal form for `preordToPartOrdCompToDualIsoToDualCompPreordToPartOrd_inv_app_hom_coe`
@[simp]
lemma preordToPartOrdCompToDualIsoToDualCompPreordToPartOrd_inv_app_hom_coe' (X)
    (a : preordToPartOrd.obj (Preord.dual.obj X)) :
    (PartOrd.Hom.hom
        (X := preordToPartOrd.obj (Preord.dual.obj X))
        (Y := PartOrd.dual.obj (preordToPartOrd.obj X))
        (preordToPartOrdCompToDualIsoToDualCompPreordToPartOrd.inv.app X)) a =
      (OrderIso.dualAntisymmetrization ‚ÜëX).symm a :=
  rfl
