/-
Copyright (c) 2022 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies
-/
import Mathlib.Order.Category.PartOrd
import Mathlib.Order.Hom.Lattice

/-!
# The category of lattices

This defines `Lat`, the category of lattices.

Note that `Lat` doesn't correspond to the literature definition of [`Lat`]
(https://ncatlab.org/nlab/show/Lat) as we don't require bottom or top elements. Instead, `Lat`
corresponds to `BddLat`.

## TODO

The free functor from `Lat` to `BddLat` is `X ‚Üí WithTop (WithBot X)`.
-/

universe u

open CategoryTheory

/-- The category of lattices. -/
structure Lat where
  /-- The underlying lattices. -/
  (carrier : Type*)
  [str : Lattice carrier]

attribute [instance] Lat.str

initialize_simps_projections Lat (carrier ‚Üí coe, -str)

namespace Lat

instance : CoeSort Lat (Type _) :=
  ‚ü®Lat.carrier‚ü©

attribute [coe] Lat.carrier

/-- Construct a bundled `Lat` from the underlying type and typeclass. -/
abbrev of (X : Type*) [Lattice X] : Lat := ‚ü®X‚ü©

/-- The type of morphisms in `Lat R`. -/
@[ext]
structure Hom (X Y : Lat.{u}) where
  private mk ::
  /-- The underlying `LatticeHom`. -/
  hom' : LatticeHom X Y

instance : Category Lat.{u} where
  Hom X Y := Hom X Y
  id X := ‚ü®LatticeHom.id X‚ü©
  comp f g := ‚ü®g.hom'.comp f.hom'‚ü©

instance : ConcreteCategory Lat (LatticeHom ¬∑ ¬∑) where
  hom := Hom.hom'
  ofHom := Hom.mk

/-- Turn a morphism in `Lat` back into a `LatticeHom`. -/
abbrev Hom.hom {X Y : Lat.{u}} (f : Hom X Y) :=
  ConcreteCategory.hom (C := Lat) f

/-- Typecheck a `LatticeHom` as a morphism in `Lat`. -/
abbrev ofHom {X Y : Type u} [Lattice X] [Lattice Y] (f : LatticeHom X Y) : of X ‚ü∂ of Y :=
  ConcreteCategory.ofHom (C := Lat) f

variable {R} in
/-- Use the `ConcreteCategory.hom` projection for `@[simps]` lemmas. -/
def Hom.Simps.hom (X Y : Lat.{u}) (f : Hom X Y) :=
  f.hom

initialize_simps_projections Hom (hom' ‚Üí hom)

/-!
The results below duplicate the `ConcreteCategory` simp lemmas, but we can keep them for `dsimp`.
-/

@[simp]
lemma coe_id {X : Lat} : (ùüô X : X ‚Üí X) = id := rfl

@[simp]
lemma coe_comp {X Y Z : Lat} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z} : (f ‚â´ g : X ‚Üí Z) = g ‚àò f := rfl

@[simp]
lemma forget_map {X Y : Lat} (f : X ‚ü∂ Y) :
    (forget Lat).map f = f := rfl

@[ext]
lemma ext {X Y : Lat} {f g : X ‚ü∂ Y} (w : ‚àÄ x : X, f x = g x) : f = g :=
  ConcreteCategory.hom_ext _ _ w

-- This is not `simp` to avoid rewriting in types of terms.
theorem coe_of (X : Type u) [Lattice X] : (Lat.of X : Type u) = X := rfl

@[simp]
lemma hom_id {X : Lat} : (ùüô X : X ‚ü∂ X).hom = LatticeHom.id _ := rfl

/- Provided for rewriting. -/
lemma id_apply (X : Lat) (x : X) :
    (ùüô X : X ‚ü∂ X) x = x := by simp

@[simp]
lemma hom_comp {X Y Z : Lat} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).hom = g.hom.comp f.hom := rfl

/- Provided for rewriting. -/
lemma comp_apply {X Y Z : Lat} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) :
    (f ‚â´ g) x = g (f x) := by simp

@[ext]
lemma hom_ext {X Y : Lat} {f g : X ‚ü∂ Y} (hf : f.hom = g.hom) : f = g :=
  Hom.ext hf

@[simp]
lemma hom_ofHom {X Y : Type u} [Lattice X] [Lattice Y] (f : LatticeHom X Y) : (ofHom f).hom = f :=
  rfl

@[simp]
lemma ofHom_hom {X Y : Lat} (f : X ‚ü∂ Y) :
    ofHom (Hom.hom f) = f := rfl

@[simp]
lemma ofHom_id {X : Type u} [Lattice X] : ofHom (LatticeHom.id _) = ùüô (of X) := rfl

@[simp]
lemma ofHom_comp {X Y Z : Type u} [Lattice X] [Lattice Y] [Lattice Z]
    (f : LatticeHom X Y) (g : LatticeHom Y Z) :
    ofHom (g.comp f) = ofHom f ‚â´ ofHom g :=
  rfl

lemma ofHom_apply {X Y : Type u} [Lattice X] [Lattice Y] (f : LatticeHom X Y) (x : X) :
    (ofHom f) x = f x := rfl

lemma inv_hom_apply {X Y : Lat} (e : X ‚âÖ Y) (x : X) : e.inv (e.hom x) = x := by
  simp

lemma hom_inv_apply {X Y : Lat} (e : X ‚âÖ Y) (s : Y) : e.hom (e.inv s) = s := by
  simp

instance hasForgetToPartOrd : HasForget‚ÇÇ Lat PartOrd where
  forget‚ÇÇ.obj X := .of X
  forget‚ÇÇ.map f := PartOrd.ofHom f.hom

/-- Constructs an isomorphism of lattices from an order isomorphism between them. -/
@[simps]
def Iso.mk {Œ± Œ≤ : Lat.{u}} (e : Œ± ‚âÉo Œ≤) : Œ± ‚âÖ Œ≤ where
  hom := ofHom e
  inv := ofHom e.symm

/-- `OrderDual` as a functor. -/
@[simps map]
def dual : Lat ‚•§ Lat where
  obj X := of X·µí·µà
  map f := ofHom f.hom.dual

/-- The equivalence between `Lat` and itself induced by `OrderDual` both ways. -/
@[simps functor inverse]
def dualEquiv : Lat ‚âå Lat where
  functor := dual
  inverse := dual
  unitIso := NatIso.ofComponents fun X ‚Ü¶ Iso.mk <| OrderIso.dualDual X
  counitIso := NatIso.ofComponents fun X ‚Ü¶ Iso.mk <| OrderIso.dualDual X

end Lat

theorem Lat_dual_comp_forget_to_partOrd :
    Lat.dual ‚ãô forget‚ÇÇ Lat PartOrd = forget‚ÇÇ Lat PartOrd ‚ãô PartOrd.dual :=
  rfl
