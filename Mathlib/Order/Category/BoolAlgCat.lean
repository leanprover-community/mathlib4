/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Order.Category.HeytAlgCat

#align_import order.category.BoolAlg from "leanprover-community/mathlib"@"e8ac6315bcfcbaf2d19a046719c3b553206dac75"

/-!
# The category of boolean algebras

This defines `BoolAlgCat`, the category of boolean algebras.
-/

set_option linter.uppercaseLean3 false

open OrderDual Opposite Set

universe u

open CategoryTheory

/-- The category of boolean algebras. -/
def BoolAlgCat :=
  Bundled BooleanAlgebra
#align BoolAlg BoolAlgCat

namespace BoolAlgCat

instance : CoeSort BoolAlgCat (Type*) :=
  Bundled.coeSort

instance instBooleanAlgebra (X : BoolAlgCat) : BooleanAlgebra X :=
  X.str

/-- Construct a bundled `BoolAlgCat` from a `BooleanAlgebra`. -/
def of (Î± : Type*) [BooleanAlgebra Î±] : BoolAlgCat :=
  Bundled.of Î±
#align BoolAlg.of BoolAlgCat.of

@[simp]
theorem coe_of (Î± : Type*) [BooleanAlgebra Î±] : â†¥(of Î±) = Î± :=
  rfl
#align BoolAlg.coe_of BoolAlgCat.coe_of

instance : Inhabited BoolAlgCat :=
  âŸ¨of PUnitâŸ©

/-- Turn a `BoolAlg` into a `BddDistLat` by forgetting its complement operation. -/
def toBddDistLatCat (X : BoolAlgCat) : BddDistLatCat :=
  BddDistLatCat.of X
#align BoolAlg.to_BddDistLat BoolAlgCat.toBddDistLatCat

@[simp]
theorem coe_toBddDistLatCat (X : BoolAlgCat) : â†¥X.toBddDistLatCat = â†¥X :=
  rfl
#align BoolAlg.coe_to_BddDistLat BoolAlgCat.coe_toBddDistLatCat

instance : LargeCategory.{u} BoolAlgCat :=
  InducedCategory.category toBddDistLatCat

instance : ConcreteCategory BoolAlgCat :=
  InducedCategory.concreteCategory toBddDistLatCat

instance hasForgetToBddDistLatCat : HasForgetâ‚‚ BoolAlgCat BddDistLatCat :=
  InducedCategory.hasForgetâ‚‚ toBddDistLatCat
#align BoolAlg.has_forget_to_BddDistLat BoolAlgCat.hasForgetToBddDistLatCat

section

attribute [local instance] BoundedLatticeHomClass.toBiheytingHomClass

@[simps]
instance hasForgetToHeytAlgCat : HasForgetâ‚‚ BoolAlgCat HeytAlgCat where
  forgetâ‚‚ :=
    { obj := fun X => {Î± := X}
      -- Porting note: was `fun {X Y} f => show BoundedLatticeHom X Y from f`
      -- which already looks like a hack, but I don't understand why this hack works now and
      -- the old one didn't
      map := fun {X Y} (f : BoundedLatticeHom X Y) => show HeytingHom X Y from f }
#align BoolAlg.has_forget_to_HeytAlg BoolAlgCat.hasForgetToHeytAlgCat

end

/-- Constructs an equivalence between Boolean algebras from an order isomorphism between them. -/
@[simps]
def Iso.mk {Î± Î² : BoolAlgCat.{u}} (e : Î± â‰ƒo Î²) : Î± â‰… Î² where
  hom := (e : BoundedLatticeHom Î± Î²)
  inv := (e.symm : BoundedLatticeHom Î² Î±)
  hom_inv_id := by ext; exact e.symm_apply_apply _
                   -- âŠ¢ â†‘((let src := { toSupHom := { toFun := â†‘e, map_sup' := (_ : âˆ€ (a b : â†‘Î±), â†‘e â€¦
                        -- ðŸŽ‰ no goals
  inv_hom_id := by ext; exact e.apply_symm_apply _
                   -- âŠ¢ â†‘((let src := { toSupHom := { toFun := â†‘(OrderIso.symm e), map_sup' := (_ :  â€¦
                        -- ðŸŽ‰ no goals
#align BoolAlg.iso.mk BoolAlgCat.Iso.mk

/-- `OrderDual` as a functor. -/
@[simps]
def dual : BoolAlgCat â¥¤ BoolAlgCat where
  obj X := of Xáµ’áµˆ
  map {X Y} := BoundedLatticeHom.dual
#align BoolAlg.dual BoolAlgCat.dual

/-- The equivalence between `BoolAlgCat` and itself induced by `OrderDual` both ways. -/
@[simps functor inverse]
def dualEquiv : BoolAlgCat â‰Œ BoolAlgCat where
  functor := dual
  inverse := dual
  unitIso := NatIso.ofComponents fun X => Iso.mk <| OrderIso.dualDual X
  counitIso := NatIso.ofComponents fun X => Iso.mk <| OrderIso.dualDual X
#align BoolAlg.dual_equiv BoolAlgCat.dualEquiv

end BoolAlgCat

theorem boolAlgCat_dual_comp_forget_to_bddDistLatCat :
    BoolAlgCat.dual â‹™ forgetâ‚‚ BoolAlgCat BddDistLatCat =
    forgetâ‚‚ BoolAlgCat BddDistLatCat â‹™ BddDistLatCat.dual :=
  rfl
#align BoolAlg_dual_comp_forget_to_BddDistLat boolAlgCat_dual_comp_forget_to_bddDistLatCat
