/-
Copyright (c) 2022 YaÃ«l Dillies, Violeta HernÃ¡ndez Palacios. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Violeta HernÃ¡ndez Palacios, Grayson Burton, Vladimir Ivanov
-/
import Mathlib.Data.Int.SuccPred
import Mathlib.Order.Fin.Basic

/-!
# Graded orders

This file defines graded orders, also known as ranked orders.

An `ğ•†`-graded order is an order `Î±` equipped with a distinguished "grade" function `Î± â†’ ğ•†` which
should be understood as giving the "height" of the elements. Usual graded orders are `â„•`-graded,
cograded orders are `â„•áµ’áµˆ`-graded, but we can also grade by `â„¤`, and polytopes are naturally
`Fin n`-graded.

Visually, `grade â„• a` is the height of `a` in the Hasse diagram of `Î±`.

## Main declarations

* `GradeOrder`: Graded order.
* `GradeMinOrder`: Graded order where minimal elements have minimal grades.
* `GradeMaxOrder`: Graded order where maximal elements have maximal grades.
* `GradeBoundedOrder`: Graded order where minimal elements have minimal grades and maximal
  elements have maximal grades.
* `grade`: The grade of an element. Because an order can admit several gradings, the first argument
  is the order we grade by.

## How to grade your order

Here are the translations between common references and our `GradeOrder`:
* [Stanley][stanley2012] defines a graded order of rank `n` as an order where all maximal chains
  have "length" `n` (so the number of elements of a chain is `n + 1`). This corresponds to
  `GradeBoundedOrder (Fin (n + 1)) Î±`.
* [Engel][engel1997]'s ranked orders are somewhere between `GradeOrder â„• Î±` and
  `GradeMinOrder â„• Î±`, in that he requires `âˆƒ a, IsMin a âˆ§ grade â„• a = 0` rather than
  `âˆ€ a, IsMin a â†’ grade â„• a = 0`. He defines a graded order as an order where all minimal elements
  have grade `0` and all maximal elements have the same grade. This is roughly a less bundled
  version of `GradeBoundedOrder (Fin n) Î±`, assuming we discard orders with infinite chains.

## Implementation notes

One possible definition of graded orders is as the bounded orders whose flags (maximal chains)
all have the same finite length (see Stanley p. 99). However, this means that all graded orders must
have minimal and maximal elements and that the grade is not data.

Instead, we define graded orders by their grade function, without talking about flags yet.

## References

* [Konrad Engel, *Sperner Theory*][engel1997]
* [Richard Stanley, *Enumerative Combinatorics*][stanley2012]
-/

open Nat OrderDual

variable {ğ•† â„™ Î± Î² : Type*}

/-- An `ğ•†`-graded order is an order `Î±` equipped with a strictly monotone function
`grade ğ•† : Î± â†’ ğ•†` which preserves order covering (`CovBy`). -/
class GradeOrder (ğ•† Î± : Type*) [Preorder ğ•†] [Preorder Î±] where
  /-- The grading function. -/
  protected grade : Î± â†’ ğ•†
  /-- `grade` is strictly monotonic. -/
  grade_strictMono : StrictMono grade
  /-- `grade` preserves `CovBy`. -/
  covBy_grade â¦ƒa b : Î±â¦„ : a â‹– b â†’ grade a â‹– grade b

/-- An `ğ•†`-graded order where minimal elements have minimal grades. -/
class GradeMinOrder (ğ•† Î± : Type*) [Preorder ğ•†] [Preorder Î±] extends GradeOrder ğ•† Î± where
  /-- Minimal elements have minimal grades. -/
  isMin_grade â¦ƒa : Î±â¦„ : IsMin a â†’ IsMin (grade a)

/-- An `ğ•†`-graded order where maximal elements have maximal grades. -/
class GradeMaxOrder (ğ•† Î± : Type*) [Preorder ğ•†] [Preorder Î±] extends GradeOrder ğ•† Î± where
  /-- Maximal elements have maximal grades. -/
  isMax_grade â¦ƒa : Î±â¦„ : IsMax a â†’ IsMax (grade a)

/-- An `ğ•†`-graded order where minimal elements have minimal grades and maximal elements have maximal
grades. -/
class GradeBoundedOrder (ğ•† Î± : Type*) [Preorder ğ•†] [Preorder Î±] extends GradeMinOrder ğ•† Î±,
  GradeMaxOrder ğ•† Î±

section Preorder -- grading
variable [Preorder ğ•†]

section Preorder -- graded order
variable [Preorder Î±]

section GradeOrder
variable (ğ•†)
variable [GradeOrder ğ•† Î±] {a b : Î±}

/-- The grade of an element in a graded order. Morally, this is the number of elements you need to
go down by to get to `âŠ¥`. -/
def grade : Î± â†’ ğ•† :=
  GradeOrder.grade

protected theorem CovBy.grade (h : a â‹– b) : grade ğ•† a â‹– grade ğ•† b :=
  GradeOrder.covBy_grade h

variable {ğ•†}

theorem grade_strictMono : StrictMono (grade ğ•† : Î± â†’ ğ•†) :=
  GradeOrder.grade_strictMono

theorem covBy_iff_lt_covBy_grade : a â‹– b â†” a < b âˆ§ grade ğ•† a â‹– grade ğ•† b :=
  âŸ¨fun h => âŸ¨h.1, h.grade _âŸ©,
    And.imp_right fun h _ ha hb => h.2 (grade_strictMono ha) <| grade_strictMono hbâŸ©

end GradeOrder

section GradeMinOrder

variable (ğ•†)
variable [GradeMinOrder ğ•† Î±] {a : Î±}

protected theorem IsMin.grade (h : IsMin a) : IsMin (grade ğ•† a) :=
  GradeMinOrder.isMin_grade h

variable {ğ•†}

@[simp]
theorem isMin_grade_iff : IsMin (grade ğ•† a) â†” IsMin a :=
  âŸ¨grade_strictMono.isMin_of_apply, IsMin.grade _âŸ©

end GradeMinOrder

section GradeMaxOrder

variable (ğ•†)
variable [GradeMaxOrder ğ•† Î±] {a : Î±}

protected theorem IsMax.grade (h : IsMax a) : IsMax (grade ğ•† a) :=
  GradeMaxOrder.isMax_grade h

variable {ğ•†}

@[simp]
theorem isMax_grade_iff : IsMax (grade ğ•† a) â†” IsMax a :=
  âŸ¨grade_strictMono.isMax_of_apply, IsMax.grade _âŸ©

end GradeMaxOrder

end Preorder

-- graded order
theorem grade_mono [PartialOrder Î±] [GradeOrder ğ•† Î±] : Monotone (grade ğ•† : Î± â†’ ğ•†) :=
  grade_strictMono.monotone

section LinearOrder

-- graded order
variable [LinearOrder Î±] [GradeOrder ğ•† Î±] {a b : Î±}

theorem grade_injective : Function.Injective (grade ğ•† : Î± â†’ ğ•†) :=
  grade_strictMono.injective

@[simp]
theorem grade_le_grade_iff : grade ğ•† a â‰¤ grade ğ•† b â†” a â‰¤ b :=
  grade_strictMono.le_iff_le

@[simp]
theorem grade_lt_grade_iff : grade ğ•† a < grade ğ•† b â†” a < b :=
  grade_strictMono.lt_iff_lt

@[simp]
theorem grade_eq_grade_iff : grade ğ•† a = grade ğ•† b â†” a = b :=
  grade_injective.eq_iff

theorem grade_ne_grade_iff : grade ğ•† a â‰  grade ğ•† b â†” a â‰  b :=
  grade_injective.ne_iff

theorem grade_covBy_grade_iff : grade ğ•† a â‹– grade ğ•† b â†” a â‹– b :=
  (covBy_iff_lt_covBy_grade.trans <| and_iff_right_of_imp fun h => grade_lt_grade_iff.1 h.1).symm

end LinearOrder

-- graded order
end Preorder

-- grading
section PartialOrder

variable [PartialOrder ğ•†] [Preorder Î±]

@[simp]
theorem grade_bot [OrderBot ğ•†] [OrderBot Î±] [GradeMinOrder ğ•† Î±] : grade ğ•† (âŠ¥ : Î±) = âŠ¥ :=
  (isMin_bot.grade _).eq_bot

@[simp]
theorem grade_top [OrderTop ğ•†] [OrderTop Î±] [GradeMaxOrder ğ•† Î±] : grade ğ•† (âŠ¤ : Î±) = âŠ¤ :=
  (isMax_top.grade _).eq_top

end PartialOrder

/-! ### Instances -/

section Preorder
variable [Preorder ğ•†] [Preorder â„™] [Preorder Î±] [Preorder Î²]

instance Preorder.toGradeBoundedOrder : GradeBoundedOrder Î± Î± where
  grade := id
  isMin_grade _ := id
  isMax_grade _ := id
  grade_strictMono := strictMono_id
  covBy_grade _ _ := id

@[simp]
theorem grade_self (a : Î±) : grade Î± a = a :=
  rfl

/-! #### Dual -/

instance OrderDual.gradeOrder [GradeOrder ğ•† Î±] : GradeOrder ğ•†áµ’áµˆ Î±áµ’áµˆ where
  grade := toDual âˆ˜ grade ğ•† âˆ˜ ofDual
  grade_strictMono := grade_strictMono.dual
  covBy_grade _ _ h := (h.ofDual.grade _).toDual

instance OrderDual.gradeMinOrder [GradeMaxOrder ğ•† Î±] : GradeMinOrder ğ•†áµ’áµˆ Î±áµ’áµˆ :=
  { OrderDual.gradeOrder with isMin_grade := fun _ => IsMax.grade (Î± := Î±) ğ•† }

instance OrderDual.gradeMaxOrder [GradeMinOrder ğ•† Î±] : GradeMaxOrder ğ•†áµ’áµˆ Î±áµ’áµˆ :=
  { OrderDual.gradeOrder with isMax_grade := fun _ => IsMin.grade (Î± := Î±) ğ•† }

instance [GradeBoundedOrder ğ•† Î±] : GradeBoundedOrder ğ•†áµ’áµˆ Î±áµ’áµˆ :=
  { OrderDual.gradeMinOrder, OrderDual.gradeMaxOrder with }

@[simp]
theorem grade_toDual [GradeOrder ğ•† Î±] (a : Î±) : grade ğ•†áµ’áµˆ (toDual a) = toDual (grade ğ•† a) :=
  rfl

@[simp]
theorem grade_ofDual [GradeOrder ğ•† Î±] (a : Î±áµ’áµˆ) : grade ğ•† (ofDual a) = ofDual (grade ğ•†áµ’áµˆ a) :=
  rfl

/-! #### Lifting a graded order -/

-- See note [reducible non-instances]
/-- Lifts a graded order along a strictly monotone function. -/
abbrev GradeOrder.liftLeft [GradeOrder ğ•† Î±] (f : ğ•† â†’ â„™) (hf : StrictMono f)
    (hcovBy : âˆ€ a b, a â‹– b â†’ f a â‹– f b) : GradeOrder â„™ Î± where
  grade := f âˆ˜ grade ğ•†
  grade_strictMono := hf.comp grade_strictMono
  covBy_grade _ _ h := hcovBy _ _ <| h.grade _

-- See note [reducible non-instances]
/-- Lifts a graded order along a strictly monotone function. -/
abbrev GradeMinOrder.liftLeft [GradeMinOrder ğ•† Î±] (f : ğ•† â†’ â„™) (hf : StrictMono f)
    (hcovBy : âˆ€ a b, a â‹– b â†’ f a â‹– f b) (hmin : âˆ€ a, IsMin a â†’ IsMin (f a)) : GradeMinOrder â„™ Î± :=
  { GradeOrder.liftLeft f hf hcovBy with isMin_grade := fun _ ha => hmin _ <| ha.grade _ }

-- See note [reducible non-instances]
/-- Lifts a graded order along a strictly monotone function. -/
abbrev GradeMaxOrder.liftLeft [GradeMaxOrder ğ•† Î±] (f : ğ•† â†’ â„™) (hf : StrictMono f)
    (hcovBy : âˆ€ a b, a â‹– b â†’ f a â‹– f b) (hmax : âˆ€ a, IsMax a â†’ IsMax (f a)) : GradeMaxOrder â„™ Î± :=
  { GradeOrder.liftLeft f hf hcovBy with isMax_grade := fun _ ha => hmax _ <| ha.grade _ }

-- See note [reducible non-instances]
/-- Lifts a graded order along a strictly monotone function. -/
abbrev GradeBoundedOrder.liftLeft [GradeBoundedOrder ğ•† Î±] (f : ğ•† â†’ â„™) (hf : StrictMono f)
    (hcovBy : âˆ€ a b, a â‹– b â†’ f a â‹– f b) (hmin : âˆ€ a, IsMin a â†’ IsMin (f a))
    (hmax : âˆ€ a, IsMax a â†’ IsMax (f a)) : GradeBoundedOrder â„™ Î± :=
  { GradeMinOrder.liftLeft f hf hcovBy hmin, GradeMaxOrder.liftLeft f hf hcovBy hmax with }

-- See note [reducible non-instances]
/-- Lifts a graded order along a strictly monotone function. -/
abbrev GradeOrder.liftRight [GradeOrder ğ•† Î²] (f : Î± â†’ Î²) (hf : StrictMono f)
    (hcovBy : âˆ€ a b, a â‹– b â†’ f a â‹– f b) : GradeOrder ğ•† Î± where
  grade := grade ğ•† âˆ˜ f
  grade_strictMono := grade_strictMono.comp hf
  covBy_grade _ _ h := (hcovBy _ _ h).grade _

-- See note [reducible non-instances]
/-- Lifts a graded order along a strictly monotone function. -/
abbrev GradeMinOrder.liftRight [GradeMinOrder ğ•† Î²] (f : Î± â†’ Î²) (hf : StrictMono f)
    (hcovBy : âˆ€ a b, a â‹– b â†’ f a â‹– f b) (hmin : âˆ€ a, IsMin a â†’ IsMin (f a)) : GradeMinOrder ğ•† Î± :=
  { GradeOrder.liftRight f hf hcovBy with isMin_grade := fun _ ha => (hmin _ ha).grade _ }

-- See note [reducible non-instances]
/-- Lifts a graded order along a strictly monotone function. -/
abbrev GradeMaxOrder.liftRight [GradeMaxOrder ğ•† Î²] (f : Î± â†’ Î²) (hf : StrictMono f)
    (hcovBy : âˆ€ a b, a â‹– b â†’ f a â‹– f b) (hmax : âˆ€ a, IsMax a â†’ IsMax (f a)) : GradeMaxOrder ğ•† Î± :=
  { GradeOrder.liftRight f hf hcovBy with isMax_grade := fun _ ha => (hmax _ ha).grade _ }

-- See note [reducible non-instances]
/-- Lifts a graded order along a strictly monotone function. -/
abbrev GradeBoundedOrder.liftRight [GradeBoundedOrder ğ•† Î²] (f : Î± â†’ Î²) (hf : StrictMono f)
    (hcovBy : âˆ€ a b, a â‹– b â†’ f a â‹– f b) (hmin : âˆ€ a, IsMin a â†’ IsMin (f a))
    (hmax : âˆ€ a, IsMax a â†’ IsMax (f a)) : GradeBoundedOrder ğ•† Î± :=
  { GradeMinOrder.liftRight f hf hcovBy hmin, GradeMaxOrder.liftRight f hf hcovBy hmax with }

/-! #### `Fin n`-graded to `â„•`-graded to `â„¤`-graded -/


-- See note [reducible non-instances]
/-- A `Fin n`-graded order is also `â„•`-graded. We do not mark this an instance because `n` is not
inferable. -/
abbrev GradeOrder.finToNat (n : â„•) [GradeOrder (Fin n) Î±] : GradeOrder â„• Î± :=
  (GradeOrder.liftLeft (_ : Fin n â†’ â„•) Fin.val_strictMono) fun _ _ => CovBy.coe_fin

-- See note [reducible non-instances]
/-- A `Fin n`-graded order is also `â„•`-graded. We do not mark this an instance because `n` is not
inferable. -/
abbrev GradeMinOrder.finToNat (n : â„•) [GradeMinOrder (Fin n) Î±] : GradeMinOrder â„• Î± :=
  (GradeMinOrder.liftLeft (_ : Fin n â†’ â„•) Fin.val_strictMono fun _ _ => CovBy.coe_fin) fun a h => by
    cases n
    Â· exact a.elim0
    rw [h.eq_bot, Fin.bot_eq_zero]
    exact isMin_bot

instance GradeOrder.natToInt [GradeOrder â„• Î±] : GradeOrder â„¤ Î± :=
  (GradeOrder.liftLeft _ Int.natCast_strictMono) fun _ _ => CovBy.intCast

theorem GradeOrder.wellFoundedLT (ğ•† : Type*) [Preorder ğ•†] [GradeOrder ğ•† Î±]
    [WellFoundedLT ğ•†] : WellFoundedLT Î± :=
  (grade_strictMono (ğ•† := ğ•†)).wellFoundedLT

theorem GradeOrder.wellFoundedGT (ğ•† : Type*) [Preorder ğ•†] [GradeOrder ğ•† Î±]
    [WellFoundedGT ğ•†] : WellFoundedGT Î± :=
  (grade_strictMono (ğ•† := ğ•†)).wellFoundedGT

instance [GradeOrder â„• Î±] : WellFoundedLT Î± :=
  GradeOrder.wellFoundedLT â„•

instance [GradeOrder â„•áµ’áµˆ Î±] : WellFoundedGT Î± :=
  GradeOrder.wellFoundedGT â„•áµ’áµˆ

end Preorder

/-!
### Grading a flag

A flag inherits the grading of its ambient order.
-/

namespace Flag
variable [PartialOrder Î±] {s : Flag Î±} {a b : s}

@[simp, norm_cast]
lemma coe_wcovBy_coe : (a : Î±) â©¿ b â†” a â©¿ b := by
  refine and_congr_right' âŸ¨fun h c hac â†¦ h hac, fun h c hac hcb â†¦
    @h âŸ¨c, mem_iff_forall_le_or_ge.2 fun d hd â†¦ ?_âŸ© hac hcbâŸ©
  classical
  obtain hda | had := le_or_gt (âŸ¨d, hdâŸ© : s) a
  Â· exact .inr ((Subtype.coe_le_coe.2 hda).trans hac.le)
  obtain hbd | hdb := le_or_gt b âŸ¨d, hdâŸ©
  Â· exact .inl (hcb.le.trans hbd)
  Â· cases h had hdb

@[simp, norm_cast]
lemma coe_covBy_coe : (a : Î±) â‹– b â†” a â‹– b := by simp [covBy_iff_wcovBy_and_not_le]

@[simp]
lemma isMax_coe : IsMax (a : Î±) â†” IsMax a where
  mp h b hab := h hab
  mpr h b hab := by
    refine @h âŸ¨b, mem_iff_forall_le_or_ge.2 fun c hc â†¦ ?_âŸ© hab
    classical
    exact .inr <| hab.trans' <| h.isTop âŸ¨c, hcâŸ©

@[simp]
lemma isMin_coe : IsMin (a : Î±) â†” IsMin a where
  mp h b hba := h hba
  mpr h b hba := by
    refine @h âŸ¨b, mem_iff_forall_le_or_ge.2 fun c hc â†¦ ?_âŸ© hba
    classical
    exact .inl <| hba.trans <| h.isBot âŸ¨c, hcâŸ©

variable [Preorder ğ•†]

instance [GradeOrder ğ•† Î±] (s : Flag Î±) : GradeOrder ğ•† s :=
  .liftRight _ (Subtype.strictMono_coe _) fun _ _ â†¦ coe_covBy_coe.2

instance [GradeMinOrder ğ•† Î±] (s : Flag Î±) : GradeMinOrder ğ•† s :=
  .liftRight _ (Subtype.strictMono_coe _) (fun _ _ â†¦ coe_covBy_coe.2) fun _ â†¦ isMin_coe.2

instance [GradeMaxOrder ğ•† Î±] (s : Flag Î±) : GradeMaxOrder ğ•† s :=
  .liftRight _ (Subtype.strictMono_coe _) (fun _ _ â†¦ coe_covBy_coe.2) fun _ â†¦ isMax_coe.2

instance [GradeBoundedOrder ğ•† Î±] (s : Flag Î±) : GradeBoundedOrder ğ•† s :=
  .liftRight _ (Subtype.strictMono_coe _) (fun _ _ â†¦ coe_covBy_coe.2) (fun _ â†¦ isMin_coe.2)
    fun _ â†¦ isMax_coe.2

@[simp, norm_cast] lemma grade_coe [GradeOrder ğ•† Î±] (a : s) : grade ğ•† (a : Î±) = grade ğ•† a := rfl

end Flag
