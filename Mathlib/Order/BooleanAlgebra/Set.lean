/-
Copyright (c) 2014 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Leonardo de Moura
-/
module

public import Mathlib.Data.Set.Image
public import Mathlib.Order.BooleanAlgebra.Basic
public import Mathlib.Tactic.Tauto

/-!
# Boolean algebra of sets

This file proves that `Set Î±` is a Boolean algebra, and proves results about set difference and
complement.

## Notation

* `sá¶œ` for the complement of `s`

## Tags

set, sets, subset, subsets, complement
-/

@[expose] public section

assert_not_exists RelIso

open Function

namespace Set
variable {Î± Î² : Type*} {s sâ‚ sâ‚‚ t tâ‚ tâ‚‚ u : Set Î±} {a b : Î±}

instance instBooleanAlgebra : BooleanAlgebra (Set Î±) where
  __ : DistribLattice (Set Î±) := inferInstance
  __ : BooleanAlgebra (Î± â†’ Prop) := inferInstance
  compl := (Â·á¶œ)
  sdiff := (Â· \ Â·)

/-- See also `Set.sdiff_inter_right_comm`. -/
lemma inter_diff_assoc (a b c : Set Î±) : (a âˆ© b) \ c = a âˆ© (b \ c) := inf_sdiff_assoc ..

/-- See also `Set.inter_diff_assoc`. -/
lemma sdiff_inter_right_comm (s t u : Set Î±) : s \ t âˆ© u = (s âˆ© u) \ t := sdiff_inf_right_comm ..

lemma inter_sdiff_left_comm (s t u : Set Î±) : s âˆ© (t \ u) = t âˆ© (s \ u) := inf_sdiff_left_comm ..

theorem diff_union_diff_cancel (hts : t âŠ† s) (hut : u âŠ† t) : s \ t âˆª t \ u = s \ u :=
  sdiff_sup_sdiff_cancel hts hut

/-- A version of `diff_union_diff_cancel` with more general hypotheses. -/
theorem diff_union_diff_cancel' (hi : s âˆ© u âŠ† t) (hu : t âŠ† s âˆª u) : (s \ t) âˆª (t \ u) = s \ u :=
  sdiff_sup_sdiff_cancel' hi hu

theorem diff_diff_eq_sdiff_union (h : u âŠ† s) : s \ (t \ u) = s \ t âˆª u := sdiff_sdiff_eq_sdiff_sup h

theorem inter_diff_distrib_left (s t u : Set Î±) : s âˆ© (t \ u) = (s âˆ© t) \ (s âˆ© u) :=
  inf_sdiff_distrib_left _ _ _

theorem inter_diff_distrib_right (s t u : Set Î±) : (s \ t) âˆ© u = (s âˆ© u) \ (t âˆ© u) :=
  inf_sdiff_distrib_right _ _ _

theorem diff_inter_distrib_right (s t r : Set Î±) : (t âˆ© r) \ s = (t \ s) âˆ© (r \ s) :=
  inf_sdiff

/-! ### Lemmas about complement -/

theorem compl_def (s : Set Î±) : sá¶œ = { x | x âˆ‰ s } :=
  rfl

theorem mem_compl {s : Set Î±} {x : Î±} (h : x âˆ‰ s) : x âˆˆ sá¶œ :=
  h

theorem compl_setOf {Î±} (p : Î± â†’ Prop) : { a | p a }á¶œ = { a | Â¬p a } :=
  rfl

theorem notMem_of_mem_compl {s : Set Î±} {x : Î±} (h : x âˆˆ sá¶œ) : x âˆ‰ s :=
  h

theorem notMem_compl_iff {x : Î±} : x âˆ‰ sá¶œ â†” x âˆˆ s :=
  not_not

@[simp]
theorem inter_compl_self (s : Set Î±) : s âˆ© sá¶œ = âˆ… :=
  inf_compl_eq_bot

@[simp]
theorem compl_inter_self (s : Set Î±) : sá¶œ âˆ© s = âˆ… :=
  compl_inf_eq_bot

@[simp]
theorem compl_empty : (âˆ… : Set Î±)á¶œ = univ :=
  compl_bot

@[simp]
theorem compl_union (s t : Set Î±) : (s âˆª t)á¶œ = sá¶œ âˆ© tá¶œ :=
  compl_sup

theorem compl_inter (s t : Set Î±) : (s âˆ© t)á¶œ = sá¶œ âˆª tá¶œ :=
  compl_inf

@[simp]
theorem compl_univ : (univ : Set Î±)á¶œ = âˆ… :=
  compl_top

@[simp]
theorem compl_empty_iff {s : Set Î±} : sá¶œ = âˆ… â†” s = univ :=
  compl_eq_bot

@[simp]
theorem compl_univ_iff {s : Set Î±} : sá¶œ = univ â†” s = âˆ… :=
  compl_eq_top

theorem compl_ne_univ : sá¶œ â‰  univ â†” s.Nonempty :=
  compl_univ_iff.not.trans nonempty_iff_ne_empty.symm

lemma inl_compl_union_inr_compl {s : Set Î±} {t : Set Î²} :
    Sum.inl '' sá¶œ âˆª Sum.inr '' tá¶œ = (Sum.inl '' s âˆª Sum.inr '' t)á¶œ := by
  rw [compl_union]
  aesop

theorem nonempty_compl : sá¶œ.Nonempty â†” s â‰  univ :=
  (ne_univ_iff_exists_notMem s).symm

theorem union_eq_compl_compl_inter_compl (s t : Set Î±) : s âˆª t = (sá¶œ âˆ© tá¶œ)á¶œ :=
  ext fun _ => or_iff_not_and_not

theorem inter_eq_compl_compl_union_compl (s t : Set Î±) : s âˆ© t = (sá¶œ âˆª tá¶œ)á¶œ :=
  ext fun _ => and_iff_not_or_not

@[simp]
theorem union_compl_self (s : Set Î±) : s âˆª sá¶œ = univ :=
  eq_univ_iff_forall.2 fun _ => em _

@[simp]
theorem compl_union_self (s : Set Î±) : sá¶œ âˆª s = univ := by rw [union_comm, union_compl_self]

theorem compl_subset_comm : sá¶œ âŠ† t â†” tá¶œ âŠ† s :=
  @compl_le_iff_compl_le _ s _ _

theorem subset_compl_comm : s âŠ† tá¶œ â†” t âŠ† sá¶œ :=
  @le_compl_iff_le_compl _ _ _ t

@[simp]
theorem compl_subset_compl : sá¶œ âŠ† tá¶œ â†” t âŠ† s :=
  @compl_le_compl_iff_le (Set Î±) _ _ _

@[gcongr] theorem compl_subset_compl_of_subset (h : t âŠ† s) : sá¶œ âŠ† tá¶œ := compl_subset_compl.2 h

theorem subset_union_compl_iff_inter_subset {s t u : Set Î±} : s âŠ† t âˆª uá¶œ â†” s âˆ© u âŠ† t :=
  (@isCompl_compl _ u _).le_sup_right_iff_inf_left_le

theorem compl_subset_iff_union {s t : Set Î±} : sá¶œ âŠ† t â†” s âˆª t = univ :=
  Iff.symm <| eq_univ_iff_forall.trans <| forall_congr' fun _ => or_iff_not_imp_left

theorem inter_subset (a b c : Set Î±) : a âˆ© b âŠ† c â†” a âŠ† bá¶œ âˆª c :=
  forall_congr' fun _ => and_imp.trans <| imp_congr_right fun _ => imp_iff_not_or

theorem inter_compl_nonempty_iff {s t : Set Î±} : (s âˆ© tá¶œ).Nonempty â†” Â¬s âŠ† t :=
  (not_subset.trans <| exists_congr fun x => by simp).symm

lemma subset_compl_iff_disjoint_left : s âŠ† tá¶œ â†” Disjoint t s := le_compl_iff_disjoint_left
lemma subset_compl_iff_disjoint_right : s âŠ† tá¶œ â†” Disjoint s t := le_compl_iff_disjoint_right
lemma disjoint_compl_left_iff_subset : Disjoint sá¶œ t â†” t âŠ† s := disjoint_compl_left_iff
lemma disjoint_compl_right_iff_subset : Disjoint s tá¶œ â†” s âŠ† t := disjoint_compl_right_iff

alias âŸ¨_, _root_.Disjoint.subset_compl_rightâŸ© := subset_compl_iff_disjoint_right
alias âŸ¨_, _root_.Disjoint.subset_compl_leftâŸ© := subset_compl_iff_disjoint_left
alias âŸ¨_, _root_.HasSubset.Subset.disjoint_compl_leftâŸ© := disjoint_compl_left_iff_subset
alias âŸ¨_, _root_.HasSubset.Subset.disjoint_compl_rightâŸ© := disjoint_compl_right_iff_subset

@[simp] lemma nonempty_compl_of_nontrivial [Nontrivial Î±] (x : Î±) : Set.Nonempty {x}á¶œ := exists_ne x

lemma mem_compl_singleton_iff : a âˆˆ ({b} : Set Î±)á¶œ â†” a â‰  b := .rfl

lemma compl_singleton_eq (a : Î±) : {a}á¶œ = {x | x â‰  a} := rfl

@[simp]
lemma compl_ne_eq_singleton (a : Î±) : {x | x â‰  a}á¶œ = {a} := compl_compl _

@[simp]
lemma subset_compl_singleton_iff : s âŠ† {a}á¶œ â†” a âˆ‰ s := subset_compl_comm.trans singleton_subset_iff

lemma image_compl_subset {f : Î± â†’ Î²} {s : Set Î±} (H : Injective f) : f '' sá¶œ âŠ† (f '' s)á¶œ :=
  Disjoint.subset_compl_left <| by simp [disjoint_iff_inf_le, â† image_inter H]

lemma subset_image_compl {f : Î± â†’ Î²} {s : Set Î±} (H : Surjective f) : (f '' s)á¶œ âŠ† f '' sá¶œ :=
  compl_subset_iff_union.2 <| by
    rw [â† image_union]
    simp [image_univ_of_surjective H]

lemma image_compl_eq {f : Î± â†’ Î²} {s : Set Î±} (H : Bijective f) : f '' sá¶œ = (f '' s)á¶œ :=
  Subset.antisymm (image_compl_subset H.1) (subset_image_compl H.2)

@[simp] lemma preimage_compl {s : Set Î²} {f : Î± â†’ Î²} : f â»Â¹' sá¶œ = (f â»Â¹' s)á¶œ := rfl

lemma preimage_compl_eq_image_compl [BooleanAlgebra Î±] (s : Set Î±) :
    Compl.compl â»Â¹' s = Compl.compl '' s :=
  Set.ext fun x =>
    âŸ¨fun h => âŸ¨xá¶œ, h, compl_compl xâŸ©, fun h =>
      Exists.elim h fun _ hy => (compl_eq_comm.mp hy.2).symm.subst hy.1âŸ©

lemma mem_compl_image [BooleanAlgebra Î±] (t : Î±) (s : Set Î±) : t âˆˆ Compl.compl '' s â†” tá¶œ âˆˆ s := by
  simp [â† preimage_compl_eq_image_compl]

lemma compl_compl_image [BooleanAlgebra Î±] (s : Set Î±) : Compl.compl '' (Compl.compl '' s) = s := by
  rw [â† image_comp, compl_comp_compl, image_id]

lemma compl_image : image (compl : Set Î± â†’ Set Î±) = preimage compl :=
  image_eq_preimage_of_inverse compl_compl compl_compl

lemma compl_image_set_of {p : Set Î± â†’ Prop} : compl '' { s | p s } = { s | p sá¶œ } :=
  congr_fun compl_image p

@[simp]
lemma image_union_image_compl_eq_range (f : Î± â†’ Î²) : f '' s âˆª f '' sá¶œ = range f := by grind

lemma insert_image_compl_eq_range (f : Î± â†’ Î²) (x : Î±) : insert (f x) (f '' {x}á¶œ) = range f := by
  grind

@[simp]
lemma compl_range_inl : (range (Sum.inl : Î± â†’ Î± âŠ• Î²))á¶œ = range (Sum.inr : Î² â†’ Î± âŠ• Î²) :=
  isCompl_range_inl_range_inr.compl_eq

@[simp]
lemma compl_range_inr : (range (Sum.inr : Î² â†’ Î± âŠ• Î²))á¶œ = range (Sum.inl : Î± â†’ Î± âŠ• Î²) :=
  isCompl_range_inl_range_inr.symm.compl_eq

@[simp]
lemma compl_range_some (Î± : Type*) : (range (some : Î± â†’ Option Î±))á¶œ = {none} :=
  (isCompl_range_some_none Î±).compl_eq

/-! ### Lemmas about set difference -/

theorem notMem_diff_of_mem {s t : Set Î±} {x : Î±} (hx : x âˆˆ t) : x âˆ‰ s \ t := fun h => h.2 hx

theorem mem_of_mem_diff {s t : Set Î±} {x : Î±} (h : x âˆˆ s \ t) : x âˆˆ s :=
  h.left

theorem notMem_of_mem_diff {s t : Set Î±} {x : Î±} (h : x âˆˆ s \ t) : x âˆ‰ t :=
  h.right

theorem diff_eq_compl_inter {s t : Set Î±} : s \ t = tá¶œ âˆ© s := by rw [diff_eq, inter_comm]

theorem diff_nonempty {s t : Set Î±} : (s \ t).Nonempty â†” Â¬s âŠ† t :=
  inter_compl_nonempty_iff

theorem diff_subset {s t : Set Î±} : s \ t âŠ† s := show s \ t â‰¤ s from sdiff_le

theorem diff_subset_compl (s t : Set Î±) : s \ t âŠ† tá¶œ :=
  diff_eq_compl_inter â–¸ inter_subset_left

theorem union_diff_cancel' {s t u : Set Î±} (hâ‚ : s âŠ† t) (hâ‚‚ : t âŠ† u) : t âˆª u \ s = u :=
  sup_sdiff_cancel' hâ‚ hâ‚‚

theorem union_diff_cancel {s t : Set Î±} (h : s âŠ† t) : s âˆª t \ s = t :=
  sup_sdiff_cancel_right h

theorem union_diff_cancel_left {s t : Set Î±} (h : s âˆ© t âŠ† âˆ…) : (s âˆª t) \ s = t :=
  Disjoint.sup_sdiff_cancel_left <| disjoint_iff_inf_le.2 h

theorem union_diff_cancel_right {s t : Set Î±} (h : s âˆ© t âŠ† âˆ…) : (s âˆª t) \ t = s :=
  Disjoint.sup_sdiff_cancel_right <| disjoint_iff_inf_le.2 h

@[simp]
theorem union_diff_left {s t : Set Î±} : (s âˆª t) \ s = t \ s :=
  sup_sdiff_left_self

@[simp]
theorem union_diff_right {s t : Set Î±} : (s âˆª t) \ t = s \ t :=
  sup_sdiff_right_self

theorem union_diff_distrib {s t u : Set Î±} : (s âˆª t) \ u = s \ u âˆª t \ u :=
  sup_sdiff

@[simp]
theorem inter_diff_self (a b : Set Î±) : a âˆ© (b \ a) = âˆ… :=
  inf_sdiff_self_right

@[simp]
theorem inter_union_diff (s t : Set Î±) : s âˆ© t âˆª s \ t = s :=
  sup_inf_sdiff s t

@[simp]
theorem diff_union_inter (s t : Set Î±) : s \ t âˆª s âˆ© t = s := by
  rw [union_comm]
  exact sup_inf_sdiff _ _

@[simp]
theorem inter_union_compl (s t : Set Î±) : s âˆ© t âˆª s âˆ© tá¶œ = s :=
  inter_union_diff _ _

theorem subset_inter_union_compl_left (s t : Set Î±) : t âŠ† s âˆ© t âˆª sá¶œ := by
  simp [inter_union_distrib_right]

theorem subset_inter_union_compl_right (s t : Set Î±) : s âŠ† s âˆ© t âˆª tá¶œ := by
  simp [inter_union_distrib_right]

theorem union_inter_compl_left_subset (s t : Set Î±) : (s âˆª t) âˆ© sá¶œ âŠ† t := by
  simp [union_inter_distrib_right]

theorem union_inter_compl_right_subset (s t : Set Î±) : (s âˆª t) âˆ© tá¶œ âŠ† s := by
  simp [union_inter_distrib_right]

@[gcongr]
theorem diff_subset_diff {sâ‚ sâ‚‚ tâ‚ tâ‚‚ : Set Î±} : sâ‚ âŠ† sâ‚‚ â†’ tâ‚‚ âŠ† tâ‚ â†’ sâ‚ \ tâ‚ âŠ† sâ‚‚ \ tâ‚‚ :=
  show sâ‚ â‰¤ sâ‚‚ â†’ tâ‚‚ â‰¤ tâ‚ â†’ sâ‚ \ tâ‚ â‰¤ sâ‚‚ \ tâ‚‚ from sdiff_le_sdiff

theorem diff_subset_diff_left {sâ‚ sâ‚‚ t : Set Î±} (h : sâ‚ âŠ† sâ‚‚) : sâ‚ \ t âŠ† sâ‚‚ \ t :=
  sdiff_le_sdiff_right â€¹sâ‚ â‰¤ sâ‚‚â€º

theorem diff_subset_diff_right {s t u : Set Î±} (h : t âŠ† u) : s \ u âŠ† s \ t :=
  sdiff_le_sdiff_left â€¹t â‰¤ uâ€º

theorem diff_subset_diff_iff_subset {r : Set Î±} (hs : s âŠ† r) (ht : t âŠ† r) :
    r \ s âŠ† r \ t â†” t âŠ† s :=
  sdiff_le_sdiff_iff_le hs ht

theorem compl_eq_univ_diff (s : Set Î±) : sá¶œ = univ \ s :=
  top_sdiff.symm

@[simp]
theorem empty_diff (s : Set Î±) : (âˆ… \ s : Set Î±) = âˆ… :=
  bot_sdiff

theorem diff_eq_empty {s t : Set Î±} : s \ t = âˆ… â†” s âŠ† t :=
  sdiff_eq_bot_iff

@[simp]
theorem diff_empty {s : Set Î±} : s \ âˆ… = s :=
  sdiff_bot

@[simp]
theorem diff_univ (s : Set Î±) : s \ univ = âˆ… :=
  diff_eq_empty.2 (subset_univ s)

theorem diff_diff {u : Set Î±} : (s \ t) \ u = s \ (t âˆª u) :=
  sdiff_sdiff_left

-- the following statement contains parentheses to help the reader
theorem diff_diff_comm {s t u : Set Î±} : (s \ t) \ u = (s \ u) \ t :=
  sdiff_sdiff_comm

theorem diff_subset_iff {s t u : Set Î±} : s \ t âŠ† u â†” s âŠ† t âˆª u :=
  show s \ t â‰¤ u â†” s â‰¤ t âˆª u from sdiff_le_iff

theorem subset_diff_union (s t : Set Î±) : s âŠ† s \ t âˆª t :=
  show s â‰¤ s \ t âˆª t from le_sdiff_sup

theorem diff_union_of_subset {s t : Set Î±} (h : t âŠ† s) : s \ t âˆª t = s :=
  Subset.antisymm (union_subset diff_subset h) (subset_diff_union _ _)

theorem diff_subset_comm {s t u : Set Î±} : s \ t âŠ† u â†” s \ u âŠ† t :=
  show s \ t â‰¤ u â†” s \ u â‰¤ t from sdiff_le_comm

theorem diff_inter {s t u : Set Î±} : s \ (t âˆ© u) = s \ t âˆª s \ u :=
  sdiff_inf

theorem diff_inter_diff : s \ t âˆ© (s \ u) = s \ (t âˆª u) :=
  sdiff_sup.symm

theorem diff_compl : s \ tá¶œ = s âˆ© t :=
  sdiff_compl

theorem compl_diff : (t \ s)á¶œ = s âˆª tá¶œ :=
  Eq.trans compl_sdiff himp_eq

theorem diff_diff_right {s t u : Set Î±} : s \ (t \ u) = s \ t âˆª s âˆ© u :=
  sdiff_sdiff_right'

theorem inter_diff_right_comm : (s âˆ© t) \ u = s \ u âˆ© t := by
  rw [diff_eq, diff_eq, inter_right_comm]

theorem diff_inter_right_comm : (s \ u) âˆ© t = (s âˆ© t) \ u := by
  rw [diff_eq, diff_eq, inter_right_comm]

@[simp]
theorem union_diff_self {s t : Set Î±} : s âˆª t \ s = s âˆª t :=
  sup_sdiff_self _ _

@[simp]
theorem diff_union_self {s t : Set Î±} : s \ t âˆª t = s âˆª t :=
  sdiff_sup_self _ _

@[simp]
theorem diff_inter_self {a b : Set Î±} : b \ a âˆ© a = âˆ… :=
  inf_sdiff_self_left

@[simp]
theorem diff_inter_self_eq_diff {s t : Set Î±} : s \ (t âˆ© s) = s \ t :=
  sdiff_inf_self_right _ _

@[simp]
theorem diff_self_inter {s t : Set Î±} : s \ (s âˆ© t) = s \ t :=
  sdiff_inf_self_left _ _

theorem diff_self {s : Set Î±} : s \ s = âˆ… :=
  sdiff_self

theorem diff_diff_right_self (s t : Set Î±) : s \ (s \ t) = s âˆ© t :=
  sdiff_sdiff_right_self

theorem diff_diff_cancel_left {s t : Set Î±} (h : s âŠ† t) : t \ (t \ s) = s :=
  sdiff_sdiff_eq_self h

theorem union_eq_diff_union_diff_union_inter (s t : Set Î±) : s âˆª t = s \ t âˆª t \ s âˆª s âˆ© t :=
  sup_eq_sdiff_sup_sdiff_sup_inf

@[simp] lemma sdiff_sep_self (s : Set Î±) (p : Î± â†’ Prop) : s \ {a âˆˆ s | p a} = {a âˆˆ s | Â¬ p a} :=
  diff_self_inter

lemma disjoint_sdiff_left : Disjoint (t \ s) s := disjoint_sdiff_self_left

lemma disjoint_sdiff_right : Disjoint s (t \ s) := disjoint_sdiff_self_right

-- TODO: prove this in terms of a Boolean algebra lemma
lemma disjoint_sdiff_inter : Disjoint (s \ t) (s âˆ© t) :=
  disjoint_of_subset_right inter_subset_right disjoint_sdiff_left

lemma subset_diff : s âŠ† t \ u â†” s âŠ† t âˆ§ Disjoint s u := le_iff_subset.symm.trans le_sdiff

lemma disjoint_of_subset_iff_left_eq_empty (h : s âŠ† t) : Disjoint s t â†” s = âˆ… :=
  disjoint_of_le_iff_left_eq_bot h

@[simp]
lemma diff_ssubset_left_iff : s \ t âŠ‚ s â†” (s âˆ© t).Nonempty :=
  sdiff_lt_left.trans <| by rw [not_disjoint_iff_nonempty_inter, inter_comm]

lemma _root_.HasSubset.Subset.diff_ssubset_of_nonempty (hst : s âŠ† t) (hs : s.Nonempty) :
    t \ s âŠ‚ t := by
  simpa [inter_eq_self_of_subset_right hst]

lemma ssubset_iff_sdiff_singleton : s âŠ‚ t â†” âˆƒ a âˆˆ t, s âŠ† t \ {a} := by
  simp [ssubset_iff_insert, subset_diff, insert_subset_iff]; aesop

@[simp]
lemma diff_singleton_subset_iff : s \ {a} âŠ† t â†” s âŠ† insert a t := by
  rw [â† union_singleton, union_comm]
  apply diff_subset_iff

lemma subset_diff_singleton (h : s âŠ† t) (ha : a âˆ‰ s) : s âŠ† t \ {a} :=
  subset_inter h <| subset_compl_comm.1 <| singleton_subset_iff.2 ha

lemma subset_insert_diff_singleton (x : Î±) (s : Set Î±) : s âŠ† insert x (s \ {x}) := by
  rw [â† diff_singleton_subset_iff]

lemma diff_insert_of_notMem (h : a âˆ‰ s) : s \ insert a t = s \ t := by
  grind

@[simp]
lemma insert_diff_of_mem (s) (h : a âˆˆ t) : insert a s \ t = s \ t := by
  grind

lemma insert_diff_of_notMem (s) (h : a âˆ‰ t) : insert a s \ t = insert a (s \ t) := by
  grind

lemma insert_diff_self_of_notMem (h : a âˆ‰ s) : insert a s \ {a} = s := by
  ext x; simp [and_iff_left_of_imp (ne_of_mem_of_not_mem Â· h)]

@[simp] lemma insert_diff_self_of_mem (ha : a âˆˆ s) : insert a (s \ {a}) = s := by
  ext; simp +contextual [or_and_left, em, ha]

lemma insert_diff_subset : insert a s \ t âŠ† insert a (s \ t) := by
  rintro b âŸ¨rfl | hbs, hbtâŸ© <;> simp [*]

lemma insert_erase_invOn :
    InvOn (insert a) (fun s â†¦ s \ {a}) {s : Set Î± | a âˆˆ s} {s : Set Î± | a âˆ‰ s} :=
  âŸ¨fun _s ha â†¦ insert_diff_self_of_mem ha, fun _s â†¦ insert_diff_self_of_notMemâŸ©

@[simp]
lemma diff_singleton_eq_self (h : a âˆ‰ s) : s \ {a} = s :=
  sdiff_eq_self_iff_disjoint.2 <| by simp [h]

lemma diff_singleton_ssubset : s \ {a} âŠ‚ s â†” a âˆˆ s := by simp

@[simp]
lemma insert_diff_singleton : insert a (s \ {a}) = insert a s := by
  simp [insert_eq, union_diff_self, -union_singleton, -singleton_union]

lemma insert_diff_singleton_comm (hab : a â‰  b) (s : Set Î±) :
    insert a (s \ {b}) = insert a s \ {b} := by
  simp_rw [â† union_singleton, union_diff_distrib,
    diff_singleton_eq_self (mem_singleton_iff.not.2 hab.symm)]

@[simp]
lemma insert_diff_insert : insert a (s \ insert a t) = insert a (s \ t) := by
  rw [â† union_singleton (s := t), â† diff_diff, insert_diff_singleton]

lemma mem_diff_singleton : a âˆˆ s \ {b} â†” a âˆˆ s âˆ§ a â‰  b := .rfl

lemma mem_diff_singleton_empty {t : Set (Set Î±)} : s âˆˆ t \ {âˆ…} â†” s âˆˆ t âˆ§ s.Nonempty :=
  mem_diff_singleton.trans <| and_congr_right' nonempty_iff_ne_empty.symm

lemma subset_insert_iff : s âŠ† insert a t â†” s âŠ† t âˆ¨ (a âˆˆ s âˆ§ s \ {a} âŠ† t) := by
  grind

lemma pair_diff_left (hab : a â‰  b) : ({a, b} : Set Î±) \ {a} = {b} := by
  rw [insert_diff_of_mem _ (mem_singleton a), diff_singleton_eq_self (by simpa)]

lemma pair_diff_right (hab : a â‰  b) : ({a, b} : Set Î±) \ {b} = {a} := by
  rw [pair_comm, pair_diff_left hab.symm]

lemma subset_image_diff (f : Î± â†’ Î²) (s t : Set Î±) : f '' s \ f '' t âŠ† f '' (s \ t) := by
  rw [diff_subset_iff, â† image_union, union_diff_self]
  exact image_mono subset_union_right

lemma image_diff {f : Î± â†’ Î²} (hf : Injective f) (s t : Set Î±) : f '' (s \ t) = f '' s \ f '' t :=
  Subset.antisymm
    (Subset.trans (image_inter_subset _ _ _) <| inter_subset_inter_right _ <| image_compl_subset hf)
    (subset_image_diff f s t)

@[simp] lemma preimage_diff (f : Î± â†’ Î²) (s t : Set Î²) : f â»Â¹' (s \ t) = f â»Â¹' s \ f â»Â¹' t := rfl

lemma image_diff_preimage {f : Î± â†’ Î²} {s : Set Î±} {t : Set Î²} :
    f '' (s \ f â»Â¹' t) = f '' s \ t := by simp_rw [diff_eq, â† preimage_compl, image_inter_preimage]

lemma image_compl_eq_range_diff_image {f : Î± â†’ Î²} (hf : Injective f) (s : Set Î±) :
    f '' sá¶œ = range f \ f '' s := by rw [â† image_univ, â† image_diff hf, compl_eq_univ_diff]

/-- Alias of `Set.image_compl_eq_range_sdiff_image`. -/
lemma range_diff_image {f : Î± â†’ Î²} (hf : Injective f) (s : Set Î±) : range f \ f '' s = f '' sá¶œ := by
  rw [image_compl_eq_range_diff_image hf]

lemma image_compl_preimage {f : Î± â†’ Î²} {s : Set Î²} : f '' (f â»Â¹' s)á¶œ = range f \ s := by
  rw [compl_eq_univ_diff, image_diff_preimage, image_univ]
/-! ### If-then-else for sets -/

/-- `ite` for sets: `Set.ite t s s' âˆ© t = s âˆ© t`, `Set.ite t s s' âˆ© tá¶œ = s' âˆ© tá¶œ`.
Defined as `s âˆ© t âˆª s' \ t`. -/
protected def ite (t s s' : Set Î±) : Set Î± :=
  s âˆ© t âˆª s' \ t

@[simp]
theorem ite_inter_self (t s s' : Set Î±) : t.ite s s' âˆ© t = s âˆ© t := by
  rw [Set.ite, union_inter_distrib_right, diff_inter_self, inter_assoc, inter_self, union_empty]

@[simp]
theorem ite_compl (t s s' : Set Î±) : tá¶œ.ite s s' = t.ite s' s := by
  rw [Set.ite, Set.ite, diff_compl, union_comm, diff_eq]

@[simp]
theorem ite_inter_compl_self (t s s' : Set Î±) : t.ite s s' âˆ© tá¶œ = s' âˆ© tá¶œ := by
  rw [â† ite_compl, ite_inter_self]

@[simp]
theorem ite_diff_self (t s s' : Set Î±) : t.ite s s' \ t = s' \ t :=
  ite_inter_compl_self t s s'

@[simp]
theorem ite_same (t s : Set Î±) : t.ite s s = s :=
  inter_union_diff _ _

@[simp]
theorem ite_left (s t : Set Î±) : s.ite s t = s âˆª t := by simp [Set.ite]

@[simp]
theorem ite_right (s t : Set Î±) : s.ite t s = t âˆ© s := by simp [Set.ite]

@[simp]
theorem ite_empty (s s' : Set Î±) : Set.ite âˆ… s s' = s' := by simp [Set.ite]

@[simp]
theorem ite_univ (s s' : Set Î±) : Set.ite univ s s' = s := by simp [Set.ite]

@[simp]
theorem ite_empty_left (t s : Set Î±) : t.ite âˆ… s = s \ t := by simp [Set.ite]

@[simp]
theorem ite_empty_right (t s : Set Î±) : t.ite s âˆ… = s âˆ© t := by simp [Set.ite]

theorem ite_mono (t : Set Î±) {sâ‚ sâ‚' sâ‚‚ sâ‚‚' : Set Î±} (h : sâ‚ âŠ† sâ‚‚) (h' : sâ‚' âŠ† sâ‚‚') :
    t.ite sâ‚ sâ‚' âŠ† t.ite sâ‚‚ sâ‚‚' :=
  union_subset_union (inter_subset_inter_left _ h) (inter_subset_inter_left _ h')

theorem ite_subset_union (t s s' : Set Î±) : t.ite s s' âŠ† s âˆª s' :=
  union_subset_union inter_subset_left diff_subset

theorem inter_subset_ite (t s s' : Set Î±) : s âˆ© s' âŠ† t.ite s s' :=
  ite_same t (s âˆ© s') â–¸ ite_mono _ inter_subset_left inter_subset_right

theorem ite_inter_inter (t sâ‚ sâ‚‚ sâ‚' sâ‚‚' : Set Î±) :
    t.ite (sâ‚ âˆ© sâ‚‚) (sâ‚' âˆ© sâ‚‚') = t.ite sâ‚ sâ‚' âˆ© t.ite sâ‚‚ sâ‚‚' := by
  ext x
  simp only [Set.ite, Set.mem_inter_iff, Set.mem_diff, Set.mem_union]
  tauto

theorem ite_inter (t sâ‚ sâ‚‚ s : Set Î±) : t.ite (sâ‚ âˆ© s) (sâ‚‚ âˆ© s) = t.ite sâ‚ sâ‚‚ âˆ© s := by
  rw [ite_inter_inter, ite_same]

theorem ite_inter_of_inter_eq (t : Set Î±) {sâ‚ sâ‚‚ s : Set Î±} (h : sâ‚ âˆ© s = sâ‚‚ âˆ© s) :
    t.ite sâ‚ sâ‚‚ âˆ© s = sâ‚ âˆ© s := by rw [â† ite_inter, â† h, ite_same]

theorem subset_ite {t s s' u : Set Î±} : u âŠ† t.ite s s' â†” u âˆ© t âŠ† s âˆ§ u \ t âŠ† s' := by
  simp only [subset_def, â† forall_and]
  refine forall_congr' fun x => ?_
  by_cases hx : x âˆˆ t <;> simp [*, Set.ite]

theorem ite_eq_of_subset_left (t : Set Î±) {sâ‚ sâ‚‚ : Set Î±} (h : sâ‚ âŠ† sâ‚‚) :
    t.ite sâ‚ sâ‚‚ = sâ‚ âˆª (sâ‚‚ \ t) := by
  ext x
  by_cases hx : x âˆˆ t <;> simp [*, Set.ite, or_iff_right_of_imp (@h x)]

theorem ite_eq_of_subset_right (t : Set Î±) {sâ‚ sâ‚‚ : Set Î±} (h : sâ‚‚ âŠ† sâ‚) :
    t.ite sâ‚ sâ‚‚ = (sâ‚ âˆ© t) âˆª sâ‚‚ := by
  ext x
  by_cases hx : x âˆˆ t <;> simp [*, Set.ite, or_iff_left_of_imp (@h x)]

@[simp]
lemma preimage_ite (f : Î± â†’ Î²) (s tâ‚ tâ‚‚ : Set Î²) :
    f â»Â¹' s.ite tâ‚ tâ‚‚ = (f â»Â¹' s).ite (f â»Â¹' tâ‚) (f â»Â¹' tâ‚‚) := rfl

/-! ### Lemmas about the powerset and image -/

/-- The powerset of `{a} âˆª s` is `ğ’« s` together with `{a} âˆª t` for each `t âˆˆ ğ’« s`. -/
theorem powerset_insert (s : Set Î±) (a : Î±) : ğ’« insert a s = ğ’« s âˆª insert a '' ğ’« s := by
  ext t
  refine âŸ¨fun h â†¦ ?_, by grindâŸ©
  by_cases hs : a âˆˆ t
  Â· right
    exact âŸ¨t \ {a}, by grindâŸ©
  Â· grind

theorem disjoint_powerset_insert {s : Set Î±} {a : Î±} (h : a âˆ‰ s) :
    Disjoint (ğ’« s) (insert a '' ğ’« s) := by
  rw [Set.disjoint_iff_forall_ne]
  refine fun u u_mem v v_mem â†¦ (ne_of_mem_of_not_mem' ?_
    (Set.notMem_subset (Set.subset_of_mem_powerset u_mem) h)).symm
  simp only [mem_powerset_iff, mem_image] at v_mem
  obtain âŸ¨_, _, eqâŸ© := v_mem
  simp [â† eq]

theorem powerset_insert_injOn {s : Set Î±} {a : Î±} (h : a âˆ‰ s) :
    Set.InjOn (insert a) (ğ’« s) := fun u u_mem v v_mem eq â†¦ by
  rw [Subset.antisymm_iff] at eq âŠ¢
  rwa [Set.insert_subset_insert_iff <| Set.notMem_subset ((mem_powerset_iff _ _).mp v_mem) h,
  Set.insert_subset_insert_iff <| Set.notMem_subset ((mem_powerset_iff _ _).mp u_mem) h] at eq

end Set

open Set

namespace Subtype
variable {Î± : Type*}

-- Not `@[simp]` since `simp` can prove this.
lemma preimage_coe_compl (s : Set Î±) : ((â†‘) : s â†’ Î±) â»Â¹' sá¶œ = âˆ… :=
  preimage_coe_eq_empty.2 (inter_compl_self s)

@[simp] lemma preimage_coe_compl' (s : Set Î±) : (fun x : (sá¶œ : Set Î±) â†¦ (x : Î±)) â»Â¹' s = âˆ… :=
  preimage_coe_eq_empty.2 (compl_inter_self s)

end Subtype
