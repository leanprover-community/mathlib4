/-
Copyright (c) 2020 Simon Hudon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Simon Hudon
-/
import Mathlib.Control.Monad.Basic
import Mathlib.Order.Chain
import Mathlib.Order.Hom.Order
import Mathlib.Order.Part

/-!
# Omega Complete Partial Orders

An omega-complete partial order is a partial order with a supremum
operation on increasing sequences indexed by natural numbers (which we
call `Ï‰Sup`). In this sense, it is strictly weaker than join complete
semi-lattices as only Ï‰-sized totally ordered sets have a supremum.

The concept of an omega-complete partial order (Ï‰CPO) is useful for the
formalization of the semantics of programming languages. Its notion of
supremum helps define the meaning of recursive procedures.

## Main definitions

 * class `OmegaCompletePartialOrder`
 * `ite`, `map`, `bind`, `seq` as continuous morphisms

## Instances of `OmegaCompletePartialOrder`

 * `Part`
 * every `CompleteLattice`
 * pi-types
 * product types
 * `OrderHom`
 * `ContinuousHom` (with notation â†’ğ’„)
   * an instance of `OmegaCompletePartialOrder (Î± â†’ğ’„ Î²)`
 * `ContinuousHom.ofFun`
 * `ContinuousHom.ofMono`
 * continuous functions:
   * `id`
   * `ite`
   * `const`
   * `Part.bind`
   * `Part.map`
   * `Part.seq`

## References

 * [Chain-complete posets and directed sets with applications][markowsky1976]
 * [Recursive definitions of partial functions and their computations][cadiou1972]
 * [Semantics of Programming Languages: Structures and Techniques][gunter1992]
-/

assert_not_exists OrderedCommMonoid

universe u v

open scoped Classical

namespace OmegaCompletePartialOrder

/-- A chain is a monotone sequence.

See the definition on page 114 of [gunter1992]. -/
def Chain (Î± : Type u) [Preorder Î±] :=
  â„• â†’o Î±

namespace Chain

variable {Î± : Type u} {Î² : Type v} {Î³ : Type*}
variable [Preorder Î±] [Preorder Î²] [Preorder Î³]

instance : FunLike (Chain Î±) â„• Î± := inferInstanceAs <| FunLike (â„• â†’o Î±) â„• Î±
instance : OrderHomClass (Chain Î±) â„• Î± := inferInstanceAs <| OrderHomClass (â„• â†’o Î±) â„• Î±
instance : CoeFun (Chain Î±) fun _ => â„• â†’ Î± := âŸ¨DFunLike.coeâŸ©

instance [Inhabited Î±] : Inhabited (Chain Î±) :=
  âŸ¨âŸ¨default, fun _ _ _ => le_rflâŸ©âŸ©

instance : Membership Î± (Chain Î±) :=
  âŸ¨fun a (c : â„• â†’o Î±) => âˆƒ i, a = c iâŸ©

variable (c c' : Chain Î±)
variable (f : Î± â†’o Î²)
variable (g : Î² â†’o Î³)

instance : LE (Chain Î±) where le x y := âˆ€ i, âˆƒ j, x i â‰¤ y j

lemma isChain_range : IsChain (Â· â‰¤ Â·) (Set.range c) := Monotone.isChain_range (OrderHomClass.mono c)

lemma directed : Directed (Â· â‰¤ Â·) c := directedOn_range.2 c.isChain_range.directedOn

/-- `map` function for `Chain` -/
-- Porting note: `simps` doesn't work with type synonyms
-- @[simps! (config := .asFn)]
def map : Chain Î² :=
  f.comp c

@[simp] theorem map_coe : â‡‘(map c f) = f âˆ˜ c := rfl

variable {f}

theorem mem_map (x : Î±) : x âˆˆ c â†’ f x âˆˆ Chain.map c f :=
  fun âŸ¨i, hâŸ© => âŸ¨i, h.symm â–¸ rflâŸ©

theorem exists_of_mem_map {b : Î²} : b âˆˆ c.map f â†’ âˆƒ a, a âˆˆ c âˆ§ f a = b :=
  fun âŸ¨i, hâŸ© => âŸ¨c i, âŸ¨i, rflâŸ©, h.symmâŸ©

@[simp]
theorem mem_map_iff {b : Î²} : b âˆˆ c.map f â†” âˆƒ a, a âˆˆ c âˆ§ f a = b :=
  âŸ¨exists_of_mem_map _, fun h => by
    rcases h with âŸ¨w, h, h'âŸ©
    subst b
    apply mem_map c _ hâŸ©

@[simp]
theorem map_id : c.map OrderHom.id = c :=
  OrderHom.comp_id _

theorem map_comp : (c.map f).map g = c.map (g.comp f) :=
  rfl

@[mono]
theorem map_le_map {g : Î± â†’o Î²} (h : f â‰¤ g) : c.map f â‰¤ c.map g :=
  fun i => by simp only [map_coe, Function.comp_apply]; exists i; apply h

/-- `OmegaCompletePartialOrder.Chain.zip` pairs up the elements of two chains
that have the same index. -/
-- Porting note: `simps` doesn't work with type synonyms
-- @[simps!]
def zip (câ‚€ : Chain Î±) (câ‚ : Chain Î²) : Chain (Î± Ã— Î²) :=
  OrderHom.prod câ‚€ câ‚

@[simp] theorem zip_coe (câ‚€ : Chain Î±) (câ‚ : Chain Î²) (n : â„•) : câ‚€.zip câ‚ n = (câ‚€ n, câ‚ n) := rfl

end Chain

end OmegaCompletePartialOrder

open OmegaCompletePartialOrder

-- Porting note: removed "set_option extends_priority 50"

/-- An omega-complete partial order is a partial order with a supremum
operation on increasing sequences indexed by natural numbers (which we
call `Ï‰Sup`). In this sense, it is strictly weaker than join complete
semi-lattices as only Ï‰-sized totally ordered sets have a supremum.

See the definition on page 114 of [gunter1992]. -/
class OmegaCompletePartialOrder (Î± : Type*) extends PartialOrder Î± where
  /-- The supremum of an increasing sequence -/
  Ï‰Sup : Chain Î± â†’ Î±
  /-- `Ï‰Sup` is an upper bound of the increasing sequence -/
  le_Ï‰Sup : âˆ€ c : Chain Î±, âˆ€ i, c i â‰¤ Ï‰Sup c
  /-- `Ï‰Sup` is a lower bound of the set of upper bounds of the increasing sequence -/
  Ï‰Sup_le : âˆ€ (c : Chain Î±) (x), (âˆ€ i, c i â‰¤ x) â†’ Ï‰Sup c â‰¤ x

namespace OmegaCompletePartialOrder

variable {Î± : Type u} {Î² : Type v} {Î³ : Type*}
variable [OmegaCompletePartialOrder Î±]

/-- Transfer an `OmegaCompletePartialOrder` on `Î²` to an `OmegaCompletePartialOrder` on `Î±`
using a strictly monotone function `f : Î² â†’o Î±`, a definition of Ï‰Sup and a proof that `f` is
continuous with regard to the provided `Ï‰Sup` and the Ï‰CPO on `Î±`. -/
protected abbrev lift [PartialOrder Î²] (f : Î² â†’o Î±) (Ï‰Supâ‚€ : Chain Î² â†’ Î²)
    (h : âˆ€ x y, f x â‰¤ f y â†’ x â‰¤ y) (h' : âˆ€ c, f (Ï‰Supâ‚€ c) = Ï‰Sup (c.map f)) :
    OmegaCompletePartialOrder Î² where
  Ï‰Sup := Ï‰Supâ‚€
  Ï‰Sup_le c x hx := h _ _ (by rw [h']; apply Ï‰Sup_le; intro i; apply f.monotone (hx i))
  le_Ï‰Sup c i := h _ _ (by rw [h']; apply le_Ï‰Sup (c.map f))

theorem le_Ï‰Sup_of_le {c : Chain Î±} {x : Î±} (i : â„•) (h : x â‰¤ c i) : x â‰¤ Ï‰Sup c :=
  le_trans h (le_Ï‰Sup c _)

theorem Ï‰Sup_total {c : Chain Î±} {x : Î±} (h : âˆ€ i, c i â‰¤ x âˆ¨ x â‰¤ c i) : Ï‰Sup c â‰¤ x âˆ¨ x â‰¤ Ï‰Sup c :=
  by_cases
    (fun (this : âˆ€ i, c i â‰¤ x) => Or.inl (Ï‰Sup_le _ _ this))
    (fun (this : Â¬âˆ€ i, c i â‰¤ x) =>
      have : âˆƒ i, Â¬c i â‰¤ x := by simp only [not_forall] at this âŠ¢; assumption
      let âŸ¨i, hxâŸ© := this
      have : x â‰¤ c i := (h i).resolve_left hx
      Or.inr <| le_Ï‰Sup_of_le _ this)

@[mono]
theorem Ï‰Sup_le_Ï‰Sup_of_le {câ‚€ câ‚ : Chain Î±} (h : câ‚€ â‰¤ câ‚) : Ï‰Sup câ‚€ â‰¤ Ï‰Sup câ‚ :=
  (Ï‰Sup_le _ _) fun i => by
    obtain âŸ¨_, hâŸ© := h i
    exact le_trans h (le_Ï‰Sup _ _)

theorem Ï‰Sup_le_iff (c : Chain Î±) (x : Î±) : Ï‰Sup c â‰¤ x â†” âˆ€ i, c i â‰¤ x := by
  constructor <;> intros
  Â· trans Ï‰Sup c
    Â· exact le_Ï‰Sup _ _
    Â· assumption
  exact Ï‰Sup_le _ _ â€¹_â€º

lemma isLUB_range_Ï‰Sup (c : Chain Î±) : IsLUB (Set.range c) (Ï‰Sup c) := by
  constructor
  Â· simp only [upperBounds, Set.mem_range, forall_exists_index, forall_apply_eq_imp_iff,
      Set.mem_setOf_eq]
    exact fun a â†¦ le_Ï‰Sup c a
  Â· simp only [lowerBounds, upperBounds, Set.mem_range, forall_exists_index,
      forall_apply_eq_imp_iff, Set.mem_setOf_eq]
    exact fun â¦ƒaâ¦„ a_1 â†¦ Ï‰Sup_le c a a_1

lemma Ï‰Sup_eq_of_isLUB {c : Chain Î±} {a : Î±} (h : IsLUB (Set.range c) a) : a = Ï‰Sup c := by
  rw [le_antisymm_iff]
  simp only [IsLUB, IsLeast, upperBounds, lowerBounds, Set.mem_range, forall_exists_index,
    forall_apply_eq_imp_iff, Set.mem_setOf_eq] at h
  constructor
  Â· apply h.2
    exact fun a â†¦ le_Ï‰Sup c a
  Â· rw [Ï‰Sup_le_iff]
    apply h.1

/-- A subset `p : Î± â†’ Prop` of the type closed under `Ï‰Sup` induces an
`OmegaCompletePartialOrder` on the subtype `{a : Î± // p a}`. -/
def subtype {Î± : Type*} [OmegaCompletePartialOrder Î±] (p : Î± â†’ Prop)
    (hp : âˆ€ c : Chain Î±, (âˆ€ i âˆˆ c, p i) â†’ p (Ï‰Sup c)) : OmegaCompletePartialOrder (Subtype p) :=
  OmegaCompletePartialOrder.lift (OrderHom.Subtype.val p)
    (fun c => âŸ¨Ï‰Sup _, hp (c.map (OrderHom.Subtype.val p)) fun _ âŸ¨n, qâŸ© => q.symm â–¸ (c n).2âŸ©)
    (fun _ _ h => h) (fun _ => rfl)

section Continuity

open Chain

variable [OmegaCompletePartialOrder Î²]
variable [OmegaCompletePartialOrder Î³]

/-- A monotone function `f : Î± â†’o Î²` is continuous if it distributes over Ï‰Sup.

In order to distinguish it from the (more commonly used) continuity from topology
(see `Mathlib/Topology/Basic.lean`), the present definition is often referred to as
"Scott-continuity" (referring to Dana Scott). It corresponds to continuity
in Scott topological spaces (not defined here). -/
def Continuous (f : Î± â†’o Î²) : Prop :=
  âˆ€ c : Chain Î±, f (Ï‰Sup c) = Ï‰Sup (c.map f)

/-- `Continuous' f` asserts that `f` is both monotone and continuous. -/
def Continuous' (f : Î± â†’ Î²) : Prop :=
  âˆƒ hf : Monotone f, Continuous âŸ¨f, hfâŸ©

lemma isLUB_of_scottContinuous {c : Chain Î±} {f : Î± â†’ Î²} (hf : ScottContinuous f) :
    IsLUB (Set.range (Chain.map c âŸ¨f, (ScottContinuous.monotone hf)âŸ©)) (f (Ï‰Sup c)) := by
  simp only [map_coe, OrderHom.coe_mk]
  rw [(Set.range_comp f â†‘c)]
  exact hf (Set.range_nonempty â†‘c) (IsChain.directedOn (isChain_range c)) (isLUB_range_Ï‰Sup c)

lemma ScottContinuous.continuous' {f : Î± â†’ Î²} (hf : ScottContinuous f) : Continuous' f := by
  constructor
  Â· intro c
    rw [â† (Ï‰Sup_eq_of_isLUB (isLUB_of_scottContinuous hf))]
    simp only [OrderHom.coe_mk]

theorem Continuous'.to_monotone {f : Î± â†’ Î²} (hf : Continuous' f) : Monotone f :=
  hf.fst

theorem Continuous.of_bundled (f : Î± â†’ Î²) (hf : Monotone f) (hf' : Continuous âŸ¨f, hfâŸ©) :
    Continuous' f :=
  âŸ¨hf, hf'âŸ©

theorem Continuous.of_bundled' (f : Î± â†’o Î²) (hf' : Continuous f) : Continuous' f :=
  âŸ¨f.mono, hf'âŸ©

theorem Continuous'.to_bundled (f : Î± â†’ Î²) (hf : Continuous' f) : Continuous âŸ¨f, hf.to_monotoneâŸ© :=
  hf.snd

@[simp, norm_cast]
theorem continuous'_coe : âˆ€ {f : Î± â†’o Î²}, Continuous' f â†” Continuous f
  | âŸ¨_, hfâŸ© => âŸ¨fun âŸ¨_, hcâŸ© => hc, fun hc => âŸ¨hf, hcâŸ©âŸ©

variable (f : Î± â†’o Î²) (g : Î² â†’o Î³)

theorem continuous_id : Continuous (@OrderHom.id Î± _) := by intro c; rw [c.map_id]; rfl

theorem continuous_comp (hfc : Continuous f) (hgc : Continuous g) : Continuous (g.comp f) := by
  dsimp [Continuous] at *; intro
  rw [hfc, hgc, Chain.map_comp]

theorem id_continuous' : Continuous' (@id Î±) :=
  continuous_id.of_bundled' _

theorem continuous_const (x : Î²) : Continuous (OrderHom.const Î± x) := fun c =>
  eq_of_forall_ge_iff fun z => by rw [Ï‰Sup_le_iff, Chain.map_coe, OrderHom.const_coe_coe]; simp

theorem const_continuous' (x : Î²) : Continuous' (Function.const Î± x) :=
  Continuous.of_bundled' (OrderHom.const Î± x) (continuous_const x)

end Continuity

end OmegaCompletePartialOrder

namespace Part

variable {Î± : Type u} {Î² : Type v} {Î³ : Type*}

open OmegaCompletePartialOrder

theorem eq_of_chain {c : Chain (Part Î±)} {a b : Î±} (ha : some a âˆˆ c) (hb : some b âˆˆ c) : a = b := by
  cases' ha with i ha; replace ha := ha.symm
  cases' hb with j hb; replace hb := hb.symm
  rw [eq_some_iff] at ha hb
  rcases le_total i j with hij | hji
  Â· have := c.monotone hij _ ha; apply mem_unique this hb
  Â· have := c.monotone hji _ hb; apply Eq.symm; apply mem_unique this ha
  -- Porting note: Old proof
  -- wlog h : i â‰¤ j := le_total i j using a b i j, b a j i
  -- rw [eq_some_iff] at ha hb
  -- have := c.monotone h _ ha; apply mem_unique this hb

/-- The (noncomputable) `Ï‰Sup` definition for the `Ï‰`-CPO structure on `Part Î±`. -/
protected noncomputable def Ï‰Sup (c : Chain (Part Î±)) : Part Î± :=
  if h : âˆƒ a, some a âˆˆ c then some (Classical.choose h) else none

theorem Ï‰Sup_eq_some {c : Chain (Part Î±)} {a : Î±} (h : some a âˆˆ c) : Part.Ï‰Sup c = some a :=
  have : âˆƒ a, some a âˆˆ c := âŸ¨a, hâŸ©
  have a' : some (Classical.choose this) âˆˆ c := Classical.choose_spec this
  calc
    Part.Ï‰Sup c = some (Classical.choose this) := dif_pos this
    _ = some a := congr_arg _ (eq_of_chain a' h)

theorem Ï‰Sup_eq_none {c : Chain (Part Î±)} (h : Â¬âˆƒ a, some a âˆˆ c) : Part.Ï‰Sup c = none :=
  dif_neg h

theorem mem_chain_of_mem_Ï‰Sup {c : Chain (Part Î±)} {a : Î±} (h : a âˆˆ Part.Ï‰Sup c) : some a âˆˆ c := by
  simp only [Part.Ï‰Sup] at h; split_ifs at h with h_1
  Â· have h' := Classical.choose_spec h_1
    rw [â† eq_some_iff] at h
    rw [â† h]
    exact h'
  Â· rcases h with âŸ¨âŸ¨âŸ©âŸ©

noncomputable instance omegaCompletePartialOrder :
    OmegaCompletePartialOrder (Part Î±) where
  Ï‰Sup := Part.Ï‰Sup
  le_Ï‰Sup c i := by
    intro x hx
    rw [â† eq_some_iff] at hx âŠ¢
    rw [Ï‰Sup_eq_some]
    rw [â† hx]
    exact âŸ¨i, rflâŸ©
  Ï‰Sup_le := by
    rintro c x hx a ha
    replace ha := mem_chain_of_mem_Ï‰Sup ha
    cases' ha with i ha
    apply hx i
    rw [â† ha]
    apply mem_some

section Inst

theorem mem_Ï‰Sup (x : Î±) (c : Chain (Part Î±)) : x âˆˆ Ï‰Sup c â†” some x âˆˆ c := by
  simp only [Ï‰Sup, Part.Ï‰Sup]
  constructor
  Â· split_ifs with h
    swap
    Â· rintro âŸ¨âŸ¨âŸ©âŸ©
    intro h'
    have hh := Classical.choose_spec h
    simp only [mem_some_iff] at h'
    subst x
    exact hh
  Â· intro h
    have h' : âˆƒ a : Î±, some a âˆˆ c := âŸ¨_, hâŸ©
    rw [dif_pos h']
    have hh := Classical.choose_spec h'
    rw [eq_of_chain hh h]
    simp

end Inst

end Part

namespace Pi

variable {Î± : Type*} {Î² : Î± â†’ Type*} {Î³ : Type*}

open OmegaCompletePartialOrder OmegaCompletePartialOrder.Chain

instance [âˆ€ a, OmegaCompletePartialOrder (Î² a)] :
    OmegaCompletePartialOrder (âˆ€ a, Î² a) where
  Ï‰Sup c a := Ï‰Sup (c.map (Pi.evalOrderHom a))
  Ï‰Sup_le c f hf a :=
    Ï‰Sup_le _ _ <| by
      rintro i
      apply hf
  le_Ï‰Sup c i x := le_Ï‰Sup_of_le _ <| le_rfl

namespace OmegaCompletePartialOrder

variable [âˆ€ x, OmegaCompletePartialOrder <| Î² x]
variable [OmegaCompletePartialOrder Î³]

theorem flipâ‚_continuous' (f : âˆ€ x : Î±, Î³ â†’ Î² x) (a : Î±) (hf : Continuous' fun x y => f y x) :
    Continuous' (f a) :=
  Continuous.of_bundled _ (fun _ _ h => hf.to_monotone h a) fun c => congr_fun (hf.to_bundled _ c) a

theorem flipâ‚‚_continuous' (f : Î³ â†’ âˆ€ x, Î² x) (hf : âˆ€ x, Continuous' fun g => f g x) :
    Continuous' f :=
  Continuous.of_bundled _ (fun x y h a => (hf a).to_monotone h)
    (by intro c; ext a; apply (hf a).to_bundled _ c)

end OmegaCompletePartialOrder

end Pi

namespace Prod

open OmegaCompletePartialOrder

variable {Î± : Type*} {Î² : Type*} {Î³ : Type*}
variable [OmegaCompletePartialOrder Î±]
variable [OmegaCompletePartialOrder Î²]
variable [OmegaCompletePartialOrder Î³]

/-- The supremum of a chain in the product `Ï‰`-CPO. -/
@[simps]
protected def Ï‰Sup (c : Chain (Î± Ã— Î²)) : Î± Ã— Î² :=
  (Ï‰Sup (c.map OrderHom.fst), Ï‰Sup (c.map OrderHom.snd))

@[simps! Ï‰Sup_fst Ï‰Sup_snd]
instance : OmegaCompletePartialOrder (Î± Ã— Î²) where
  Ï‰Sup := Prod.Ï‰Sup
  Ï‰Sup_le := fun _ _ h => âŸ¨Ï‰Sup_le _ _ fun i => (h i).1, Ï‰Sup_le _ _ fun i => (h i).2âŸ©
  le_Ï‰Sup c i := âŸ¨le_Ï‰Sup (c.map OrderHom.fst) i, le_Ï‰Sup (c.map OrderHom.snd) iâŸ©

theorem Ï‰Sup_zip (câ‚€ : Chain Î±) (câ‚ : Chain Î²) : Ï‰Sup (câ‚€.zip câ‚) = (Ï‰Sup câ‚€, Ï‰Sup câ‚) := by
  apply eq_of_forall_ge_iff; rintro âŸ¨zâ‚, zâ‚‚âŸ©
  simp [Ï‰Sup_le_iff, forall_and]

end Prod

open OmegaCompletePartialOrder

namespace CompleteLattice

variable (Î± : Type u)

-- see Note [lower instance priority]
/-- Any complete lattice has an `Ï‰`-CPO structure where the countable supremum is a special case
of arbitrary suprema. -/
instance (priority := 100) [CompleteLattice Î±] : OmegaCompletePartialOrder Î± where
  Ï‰Sup c := â¨† i, c i
  Ï‰Sup_le := fun âŸ¨c, _âŸ© s hs => by
    simp only [iSup_le_iff, OrderHom.coe_mk] at hs âŠ¢; intro i; apply hs i
  le_Ï‰Sup := fun âŸ¨c, _âŸ© i => by simp only [OrderHom.coe_mk]; apply le_iSup_of_le i; rfl

variable {Î±} {Î² : Type v} [OmegaCompletePartialOrder Î±] [CompleteLattice Î²]

theorem sSup_continuous (s : Set <| Î± â†’o Î²) (hs : âˆ€ f âˆˆ s, Continuous f) : Continuous (sSup s) := by
  intro c
  apply eq_of_forall_ge_iff
  intro z
  suffices (âˆ€ f âˆˆ s, âˆ€ (n), (f : _) (c n) â‰¤ z) â†” âˆ€ (n), âˆ€ f âˆˆ s, (f : _) (c n) â‰¤ z by
    simpa (config := { contextual := true }) [Ï‰Sup_le_iff, hs _ _ _] using this
  exact âŸ¨fun H n f hf => H f hf n, fun H f hf n => H n f hfâŸ©

theorem iSup_continuous {Î¹ : Sort*} {f : Î¹ â†’ Î± â†’o Î²} (h : âˆ€ i, Continuous (f i)) :
    Continuous (â¨† i, f i) :=
  sSup_continuous _ <| Set.forall_mem_range.2 h

theorem sSup_continuous' (s : Set (Î± â†’ Î²)) (hc : âˆ€ f âˆˆ s, Continuous' f) :
    Continuous' (sSup s) := by
  lift s to Set (Î± â†’o Î²) using fun f hf => (hc f hf).to_monotone
  simp only [Set.forall_mem_image, continuous'_coe] at hc
  rw [sSup_image]
  norm_cast
  exact iSup_continuous fun f â†¦ iSup_continuous fun hf â†¦ hc hf

theorem sup_continuous {f g : Î± â†’o Î²} (hf : Continuous f) (hg : Continuous g) :
    Continuous (f âŠ” g) := by
  rw [â† sSup_pair]; apply sSup_continuous
  rintro f (rfl | rfl | _) <;> assumption

theorem top_continuous : Continuous (âŠ¤ : Î± â†’o Î²) := by
  intro c; apply eq_of_forall_ge_iff; intro z
  simp only [OrderHom.instTopOrderHom_top, OrderHom.const_coe_coe, Function.const, top_le_iff,
    Ï‰Sup_le_iff, Chain.map_coe, Function.comp, forall_const]

theorem bot_continuous : Continuous (âŠ¥ : Î± â†’o Î²) := by
  rw [â† sSup_empty]
  exact sSup_continuous _ fun f hf => hf.elim

end CompleteLattice

namespace CompleteLattice

variable {Î± Î² : Type*} [OmegaCompletePartialOrder Î±] [CompleteLinearOrder Î²]

theorem inf_continuous (f g : Î± â†’o Î²) (hf : Continuous f) (hg : Continuous g) :
    Continuous (f âŠ“ g) := by
  refine fun c => eq_of_forall_ge_iff fun z => ?_
  simp only [inf_le_iff, hf c, hg c, Ï‰Sup_le_iff, â† forall_or_left, â† forall_or_right,
             Chain.map_coe, OrderHom.coe_inf, Pi.inf_apply, Function.comp]
  exact âŸ¨fun h _ â†¦ h _ _, fun h i j â†¦
    (h (max j i)).imp (le_trans <| f.mono <| c.mono <| le_max_left _ _)
      (le_trans <| g.mono <| c.mono <| le_max_right _ _)âŸ©

theorem inf_continuous' {f g : Î± â†’ Î²} (hf : Continuous' f) (hg : Continuous' g) :
    Continuous' (f âŠ“ g) :=
  âŸ¨_, inf_continuous _ _ hf.snd hg.sndâŸ©

end CompleteLattice

namespace OmegaCompletePartialOrder

variable {Î± : Type u} {Î±' : Type*} {Î² : Type v} {Î²' : Type*} {Î³ : Type*} {Ï† : Type*}
variable [OmegaCompletePartialOrder Î±] [OmegaCompletePartialOrder Î²]
variable [OmegaCompletePartialOrder Î³] [OmegaCompletePartialOrder Ï†]
variable [OmegaCompletePartialOrder Î±'] [OmegaCompletePartialOrder Î²']

namespace OrderHom

/-- The `Ï‰Sup` operator for monotone functions. -/
@[simps]
protected def Ï‰Sup (c : Chain (Î± â†’o Î²)) : Î± â†’o Î² where
  toFun a := Ï‰Sup (c.map (OrderHom.apply a))
  monotone' _ _ h := Ï‰Sup_le_Ï‰Sup_of_le ((Chain.map_le_map _) fun a => a.monotone h)

@[simps! Ï‰Sup_coe]
instance omegaCompletePartialOrder : OmegaCompletePartialOrder (Î± â†’o Î²) :=
  OmegaCompletePartialOrder.lift OrderHom.coeFnHom OrderHom.Ï‰Sup (fun _ _ h => h) fun _ => rfl

end OrderHom

section

variable (Î± Î²)

/-- A monotone function on `Ï‰`-continuous partial orders is said to be continuous
if for every chain `c : chain Î±`, `f (âŠ” i, c i) = âŠ” i, f (c i)`.
This is just the bundled version of `OrderHom.continuous`. -/
structure ContinuousHom extends OrderHom Î± Î² where
  /-- The underlying function of a `ContinuousHom` is continuous, i.e. it preserves `Ï‰Sup` -/
  cont : Continuous toOrderHom

attribute [nolint docBlame] ContinuousHom.toOrderHom

@[inherit_doc] infixr:25 " â†’ğ’„ " => ContinuousHom -- Input: \r\MIc

instance : FunLike (Î± â†’ğ’„ Î²) Î± Î² where
  coe f := f.toFun
  coe_injective' := by rintro âŸ¨âŸ© âŸ¨âŸ© h; congr; exact DFunLike.ext' h

instance : OrderHomClass (Î± â†’ğ’„ Î²) Î± Î² where
  map_rel f _ _ h := f.mono h

-- Porting note: removed to avoid conflict with the generic instance
-- instance : Coe (Î± â†’ğ’„ Î²) (Î± â†’o Î²) where coe := ContinuousHom.toOrderHom

instance : PartialOrder (Î± â†’ğ’„ Î²) :=
  (PartialOrder.lift fun f => f.toOrderHom.toFun) <| by rintro âŸ¨âŸ¨âŸ©âŸ© âŸ¨âŸ¨âŸ©âŸ© h; congr

end

namespace ContinuousHom

-- Not a `simp` lemma because in many cases projection is simpler than a generic coercion
theorem toOrderHom_eq_coe (f : Î± â†’ğ’„ Î²) : f.1 = f := rfl

@[simp] theorem coe_mk (f : Î± â†’o Î²) (hf : Continuous f) : â‡‘(mk f hf) = f := rfl
@[simp] theorem coe_toOrderHom (f : Î± â†’ğ’„ Î²) : â‡‘f.1 = f := rfl

/-- See Note [custom simps projection]. We specify this explicitly because we don't have a DFunLike
instance.
-/
def Simps.apply (h : Î± â†’ğ’„ Î²) : Î± â†’ Î² :=
  h

initialize_simps_projections ContinuousHom (toFun â†’ apply)

protected theorem congr_fun {f g : Î± â†’ğ’„ Î²} (h : f = g) (x : Î±) : f x = g x :=
  DFunLike.congr_fun h x

protected theorem congr_arg (f : Î± â†’ğ’„ Î²) {x y : Î±} (h : x = y) : f x = f y :=
  congr_arg f h

protected theorem monotone (f : Î± â†’ğ’„ Î²) : Monotone f :=
  f.monotone'

@[mono]
theorem apply_mono {f g : Î± â†’ğ’„ Î²} {x y : Î±} (hâ‚ : f â‰¤ g) (hâ‚‚ : x â‰¤ y) : f x â‰¤ g y :=
  OrderHom.apply_mono (show (f : Î± â†’o Î²) â‰¤ g from hâ‚) hâ‚‚

theorem ite_continuous' {p : Prop} [hp : Decidable p] (f g : Î± â†’ Î²) (hf : Continuous' f)
    (hg : Continuous' g) : Continuous' fun x => if p then f x else g x := by
  split_ifs <;> simp [*]

theorem Ï‰Sup_bind {Î² Î³ : Type v} (c : Chain Î±) (f : Î± â†’o Part Î²) (g : Î± â†’o Î² â†’ Part Î³) :
    Ï‰Sup (c.map (f.partBind g)) = Ï‰Sup (c.map f) >>= Ï‰Sup (c.map g) := by
  apply eq_of_forall_ge_iff; intro x
  simp only [Ï‰Sup_le_iff, Part.bind_le, Chain.mem_map_iff, and_imp, OrderHom.partBind_coe,
    exists_imp]
  constructor <;> intro h'''
  Â· intro b hb
    apply Ï‰Sup_le _ _ _
    rintro i y hy
    simp only [Part.mem_Ï‰Sup] at hb
    rcases hb with âŸ¨j, hbâŸ©
    replace hb := hb.symm
    simp only [Part.eq_some_iff, Chain.map_coe, Function.comp_apply, OrderHom.apply_coe] at hy hb
    replace hb : b âˆˆ f (c (max i j)) := f.mono (c.mono (le_max_right i j)) _ hb
    replace hy : y âˆˆ g (c (max i j)) b := g.mono (c.mono (le_max_left i j)) _ _ hy
    apply h''' (max i j)
    simp only [exists_prop, Part.bind_eq_bind, Part.mem_bind_iff, Chain.map_coe,
      Function.comp_apply, OrderHom.partBind_coe]
    exact âŸ¨_, hb, hyâŸ©
  Â· intro i
    intro y hy
    simp only [exists_prop, Part.bind_eq_bind, Part.mem_bind_iff, Chain.map_coe,
      Function.comp_apply, OrderHom.partBind_coe] at hy
    rcases hy with âŸ¨b, hbâ‚€, hbâ‚âŸ©
    apply h''' b _
    Â· apply le_Ï‰Sup (c.map g) _ _ _ hbâ‚
    Â· apply le_Ï‰Sup (c.map f) i _ hbâ‚€

theorem bind_continuous' {Î² Î³ : Type v} (f : Î± â†’ Part Î²) (g : Î± â†’ Î² â†’ Part Î³) :
    Continuous' f â†’ Continuous' g â†’ Continuous' fun x => f x >>= g x
  | âŸ¨hf, hf'âŸ©, âŸ¨hg, hg'âŸ© =>
    Continuous.of_bundled' (OrderHom.partBind âŸ¨f, hfâŸ© âŸ¨g, hgâŸ©)
      (by intro c; rw [Ï‰Sup_bind, â† hf', â† hg']; rfl)

theorem map_continuous' {Î² Î³ : Type v} (f : Î² â†’ Î³) (g : Î± â†’ Part Î²) (hg : Continuous' g) :
    Continuous' fun x => f <$> g x := by
  simp only [map_eq_bind_pure_comp]; apply bind_continuous' _ _ hg; apply const_continuous'

theorem seq_continuous' {Î² Î³ : Type v} (f : Î± â†’ Part (Î² â†’ Î³)) (g : Î± â†’ Part Î²) (hf : Continuous' f)
    (hg : Continuous' g) : Continuous' fun x => f x <*> g x := by
  simp only [seq_eq_bind_map]
  apply bind_continuous' _ _ hf
  apply Pi.OmegaCompletePartialOrder.flipâ‚‚_continuous'
  intro
  apply map_continuous' _ _ hg

theorem continuous (F : Î± â†’ğ’„ Î²) (C : Chain Î±) : F (Ï‰Sup C) = Ï‰Sup (C.map F) :=
  ContinuousHom.cont _ _

/-- Construct a continuous function from a bare function, a continuous function, and a proof that
they are equal. -/
-- Porting note: removed `@[reducible]`
@[simps!]
def copy (f : Î± â†’ Î²) (g : Î± â†’ğ’„ Î²) (h : f = g) : Î± â†’ğ’„ Î² where
  toOrderHom := g.1.copy f h
  cont := by rw [OrderHom.copy_eq]; exact g.cont

-- Porting note: `of_mono` now defeq `mk`

/-- The identity as a continuous function. -/
@[simps!]
def id : Î± â†’ğ’„ Î± := âŸ¨OrderHom.id, continuous_idâŸ©

/-- The composition of continuous functions. -/
@[simps!]
def comp (f : Î² â†’ğ’„ Î³) (g : Î± â†’ğ’„ Î²) : Î± â†’ğ’„ Î³ := âŸ¨.comp f.1 g.1, continuous_comp _ _ g.cont f.contâŸ©

@[ext]
protected theorem ext (f g : Î± â†’ğ’„ Î²) (h : âˆ€ x, f x = g x) : f = g := DFunLike.ext f g h

protected theorem coe_inj (f g : Î± â†’ğ’„ Î²) (h : (f : Î± â†’ Î²) = g) : f = g :=
  DFunLike.ext' h

@[simp]
theorem comp_id (f : Î² â†’ğ’„ Î³) : f.comp id = f := rfl

@[simp]
theorem id_comp (f : Î² â†’ğ’„ Î³) : id.comp f = f := rfl

@[simp]
theorem comp_assoc (f : Î³ â†’ğ’„ Ï†) (g : Î² â†’ğ’„ Î³) (h : Î± â†’ğ’„ Î²) : f.comp (g.comp h) = (f.comp g).comp h :=
  rfl

@[simp]
theorem coe_apply (a : Î±) (f : Î± â†’ğ’„ Î²) : (f : Î± â†’o Î²) a = f a :=
  rfl

/-- `Function.const` is a continuous function. -/
@[simps!]
def const (x : Î²) : Î± â†’ğ’„ Î² := âŸ¨.const _ x, continuous_const xâŸ©

instance [Inhabited Î²] : Inhabited (Î± â†’ğ’„ Î²) :=
  âŸ¨const defaultâŸ©

/-- The map from continuous functions to monotone functions is itself a monotone function. -/
@[simps]
def toMono : (Î± â†’ğ’„ Î²) â†’o Î± â†’o Î² where
  toFun f := f
  monotone' _ _ h := h

/-- When proving that a chain of applications is below a bound `z`, it suffices to consider the
functions and values being selected from the same index in the chains.

This lemma is more specific than necessary, i.e. `câ‚€` only needs to be a
chain of monotone functions, but it is only used with continuous functions. -/
@[simp]
theorem forall_forall_merge (câ‚€ : Chain (Î± â†’ğ’„ Î²)) (câ‚ : Chain Î±) (z : Î²) :
    (âˆ€ i j : â„•, (câ‚€ i) (câ‚ j) â‰¤ z) â†” âˆ€ i : â„•, (câ‚€ i) (câ‚ i) â‰¤ z := by
  constructor <;> introv h
  Â· apply h
  Â· apply le_trans _ (h (max i j))
    trans câ‚€ i (câ‚ (max i j))
    Â· apply (câ‚€ i).monotone
      apply câ‚.monotone
      apply le_max_right
    Â· apply câ‚€.monotone
      apply le_max_left

@[simp]
theorem forall_forall_merge' (câ‚€ : Chain (Î± â†’ğ’„ Î²)) (câ‚ : Chain Î±) (z : Î²) :
    (âˆ€ j i : â„•, (câ‚€ i) (câ‚ j) â‰¤ z) â†” âˆ€ i : â„•, (câ‚€ i) (câ‚ i) â‰¤ z := by
  rw [forall_swap, forall_forall_merge]

/-- The `Ï‰Sup` operator for continuous functions, which takes the pointwise countable supremum
of the functions in the `Ï‰`-chain. -/
@[simps!]
protected def Ï‰Sup (c : Chain (Î± â†’ğ’„ Î²)) : Î± â†’ğ’„ Î² :=
  .mk (Ï‰Sup <| c.map toMono) fun c' â†¦ by
    apply eq_of_forall_ge_iff; intro z
    simp only [Ï‰Sup_le_iff, (c _).continuous, Chain.map_coe, OrderHom.apply_coe, toMono_coe,
      OrderHom.omegaCompletePartialOrder_Ï‰Sup_coe, forall_forall_merge, OrderHomClass.coe_coe,
      forall_forall_merge', (Â· âˆ˜ Â·), Function.eval]

@[simps Ï‰Sup]
instance : OmegaCompletePartialOrder (Î± â†’ğ’„ Î²) :=
  OmegaCompletePartialOrder.lift ContinuousHom.toMono ContinuousHom.Ï‰Sup
    (fun _ _ h => h) (fun _ => rfl)

namespace Prod

/-- The application of continuous functions as a continuous function.  -/
@[simps]
def apply : (Î± â†’ğ’„ Î²) Ã— Î± â†’ğ’„ Î² where
  toFun f := f.1 f.2
  monotone' x y h := by
    dsimp
    trans y.fst x.snd <;> [apply h.1; apply y.1.monotone h.2]
  cont := by
    intro c
    apply le_antisymm
    Â· apply Ï‰Sup_le
      intro i
      dsimp
      rw [(c _).fst.continuous]
      apply Ï‰Sup_le
      intro j
      apply le_Ï‰Sup_of_le (max i j)
      apply apply_mono
      Â· exact monotone_fst (OrderHom.mono _ (le_max_left _ _))
      Â· exact monotone_snd (OrderHom.mono _ (le_max_right _ _))
    Â· apply Ï‰Sup_le
      intro i
      apply le_Ï‰Sup_of_le i
      dsimp
      apply OrderHom.mono _
      apply le_Ï‰Sup_of_le i
      rfl

end Prod

theorem Ï‰Sup_def (c : Chain (Î± â†’ğ’„ Î²)) (x : Î±) : Ï‰Sup c x = ContinuousHom.Ï‰Sup c x :=
  rfl

theorem Ï‰Sup_apply_Ï‰Sup (câ‚€ : Chain (Î± â†’ğ’„ Î²)) (câ‚ : Chain Î±) :
    Ï‰Sup câ‚€ (Ï‰Sup câ‚) = Prod.apply (Ï‰Sup (câ‚€.zip câ‚)) := by simp [Prod.apply_apply, Prod.Ï‰Sup_zip]

/-- A family of continuous functions yields a continuous family of functions. -/
@[simps]
def flip {Î± : Type*} (f : Î± â†’ Î² â†’ğ’„ Î³) : Î² â†’ğ’„ Î± â†’ Î³ where
  toFun x y := f y x
  monotone' x y h a := (f a).monotone h
  cont := by intro _; ext x; change f _ _ = _; rw [(f _).continuous]; rfl

/-- `Part.bind` as a continuous function. -/
@[simps! apply] -- Porting note: removed `(config := { rhsMd := reducible })`
noncomputable def bind {Î² Î³ : Type v} (f : Î± â†’ğ’„ Part Î²) (g : Î± â†’ğ’„ Î² â†’ Part Î³) : Î± â†’ğ’„ Part Î³ :=
  .mk (OrderHom.partBind f g.toOrderHom) fun c => by
    rw [Ï‰Sup_bind, â† f.continuous, g.toOrderHom_eq_coe, â† g.continuous]
    rfl

/-- `Part.map` as a continuous function. -/
@[simps! apply] -- Porting note: removed `(config := { rhsMd := reducible })`
noncomputable def map {Î² Î³ : Type v} (f : Î² â†’ Î³) (g : Î± â†’ğ’„ Part Î²) : Î± â†’ğ’„ Part Î³ :=
  .copy (fun x => f <$> g x) (bind g (const (pure âˆ˜ f))) <| by
    ext1
    simp only [map_eq_bind_pure_comp, bind, coe_mk, OrderHom.partBind_coe, coe_apply,
      coe_toOrderHom, const_apply, Part.bind_eq_bind]

/-- `Part.seq` as a continuous function. -/
@[simps! apply] -- Porting note: removed `(config := { rhsMd := reducible })`
noncomputable def seq {Î² Î³ : Type v} (f : Î± â†’ğ’„ Part (Î² â†’ Î³)) (g : Î± â†’ğ’„ Part Î²) : Î± â†’ğ’„ Part Î³ :=
  .copy (fun x => f x <*> g x) (bind f <| flip <| _root_.flip map g) <| by
      ext
      simp only [seq_eq_bind_map, Part.bind_eq_bind, Part.mem_bind_iff, flip_apply, _root_.flip,
        map_apply, bind_apply, Part.map_eq_map]

end ContinuousHom

end OmegaCompletePartialOrder
