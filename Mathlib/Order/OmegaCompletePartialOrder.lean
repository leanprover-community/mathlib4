/-
Copyright (c) 2020 Simon Hudon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Simon Hudon, Ira Fesefeldt
-/
import Mathlib.Control.Monad.Basic
import Mathlib.Dynamics.FixedPoints.Basic
import Mathlib.Order.CompleteLattice.Basic
import Mathlib.Order.Iterate
import Mathlib.Order.Part
import Mathlib.Order.Preorder.Chain
import Mathlib.Order.ScottContinuity

/-!
# Omega Complete Partial Orders

An omega-complete partial order is a partial order with a supremum
operation on increasing sequences indexed by natural numbers (which we
call `Ï‰Sup`). In this sense, it is strictly weaker than join complete
semi-lattices as only Ï‰-sized totally ordered sets have a supremum.

The concept of an omega-complete partial order (Ï‰CPO) is useful for the
formalization of the semantics of programming languages. Its notion of
supremum helps define the meaning of recursive procedures.

## Main definitions

* class `OmegaCompletePartialOrder`
* `ite`, `map`, `bind`, `seq` as continuous morphisms

## Instances of `OmegaCompletePartialOrder`

* `Part`
* every `CompleteLattice`
* pi-types
* product types
* `OrderHom`
* `ContinuousHom` (with notation â†’ğ’„)
  * an instance of `OmegaCompletePartialOrder (Î± â†’ğ’„ Î²)`
* `ContinuousHom.ofFun`
* `ContinuousHom.ofMono`
* continuous functions:
  * `id`
  * `ite`
  * `const`
  * `Part.bind`
  * `Part.map`
  * `Part.seq`

## References

* [Chain-complete posets and directed sets with applications][markowsky1976]
* [Recursive definitions of partial functions and their computations][cadiou1972]
* [Semantics of Programming Languages: Structures and Techniques][gunter1992]
-/

universe u v
variable {Î¹ : Sort*} {Î± Î² Î³ Î´ : Type*}

namespace OmegaCompletePartialOrder

/-- A chain is a monotone sequence.

See the definition on page 114 of [gunter1992]. -/
def Chain (Î± : Type u) [Preorder Î±] :=
  â„• â†’o Î±

namespace Chain
variable [Preorder Î±] [Preorder Î²] [Preorder Î³]

instance : FunLike (Chain Î±) â„• Î± := inferInstanceAs <| FunLike (â„• â†’o Î±) â„• Î±
instance : OrderHomClass (Chain Î±) â„• Î± := inferInstanceAs <| OrderHomClass (â„• â†’o Î±) â„• Î±

/-- See note [partially-applied ext lemmas]. -/
@[ext] lemma ext â¦ƒf g : Chain Î±â¦„ (h : â‡‘f = â‡‘g) : f = g := DFunLike.ext' h

instance [Inhabited Î±] : Inhabited (Chain Î±) :=
  âŸ¨âŸ¨default, fun _ _ _ => le_rflâŸ©âŸ©

instance : Membership Î± (Chain Î±) :=
  âŸ¨fun (c : â„• â†’o Î±) a => âˆƒ i, a = c iâŸ©

variable (c c' : Chain Î±)
variable (f : Î± â†’o Î²)
variable (g : Î² â†’o Î³)

instance : LE (Chain Î±) where le x y := âˆ€ i, âˆƒ j, x i â‰¤ y j

lemma isChain_range : IsChain (Â· â‰¤ Â·) (Set.range c) := Monotone.isChain_range (OrderHomClass.mono c)

lemma directed : Directed (Â· â‰¤ Â·) c := directedOn_range.2 c.isChain_range.directedOn

/-- `map` function for `Chain` -/
-- Not `@[simps]`: we need `@[simps!]` to see through the type synonym `Chain Î² = â„• â†’o Î²`,
-- but then we'd get the `FunLike` instance for `OrderHom` instead.
def map : Chain Î² :=
  f.comp c

@[simp] theorem map_coe : â‡‘(map c f) = f âˆ˜ c := rfl

variable {f}

theorem mem_map (x : Î±) : x âˆˆ c â†’ f x âˆˆ Chain.map c f :=
  fun âŸ¨i, hâŸ© => âŸ¨i, h.symm â–¸ rflâŸ©

theorem exists_of_mem_map {b : Î²} : b âˆˆ c.map f â†’ âˆƒ a, a âˆˆ c âˆ§ f a = b :=
  fun âŸ¨i, hâŸ© => âŸ¨c i, âŸ¨i, rflâŸ©, h.symmâŸ©

@[simp]
theorem mem_map_iff {b : Î²} : b âˆˆ c.map f â†” âˆƒ a, a âˆˆ c âˆ§ f a = b :=
  âŸ¨exists_of_mem_map _, fun h => by
    rcases h with âŸ¨w, h, h'âŸ©
    subst b
    apply mem_map c _ hâŸ©

@[simp]
theorem map_id : c.map OrderHom.id = c :=
  OrderHom.comp_id _

theorem map_comp : (c.map f).map g = c.map (g.comp f) :=
  rfl

@[mono]
theorem map_le_map {g : Î± â†’o Î²} (h : f â‰¤ g) : c.map f â‰¤ c.map g :=
  fun i => by simp only [map_coe, Function.comp_apply]; exists i; apply h

/-- `OmegaCompletePartialOrder.Chain.zip` pairs up the elements of two chains
that have the same index. -/
-- Not `@[simps]`: we need `@[simps!]` to see through the type synonym `Chain Î² = â„• â†’o Î²`,
-- but then we'd get the `FunLike` instance for `OrderHom` instead.
def zip (câ‚€ : Chain Î±) (câ‚ : Chain Î²) : Chain (Î± Ã— Î²) :=
  OrderHom.prod câ‚€ câ‚

@[simp] theorem zip_coe (câ‚€ : Chain Î±) (câ‚ : Chain Î²) (n : â„•) : câ‚€.zip câ‚ n = (câ‚€ n, câ‚ n) := rfl

/-- An example of a `Chain` constructed from an ordered pair. -/
def pair (a b : Î±) (hab : a â‰¤ b) : Chain Î± where
  toFun
    | 0 => a
    | _ => b
  monotone' _ _ _ := by aesop

@[simp] lemma pair_zero (a b : Î±) (hab) : pair a b hab 0 = a := rfl
@[simp] lemma pair_succ (a b : Î±) (hab) (n : â„•) : pair a b hab (n + 1) = b := rfl

@[simp] lemma range_pair (a b : Î±) (hab) : Set.range (pair a b hab) = {a, b} := by
  ext; exact Nat.or_exists_add_one.symm.trans (by aesop)

@[simp] lemma pair_zip_pair (aâ‚ aâ‚‚ : Î±) (bâ‚ bâ‚‚ : Î²) (ha hb) :
    (pair aâ‚ aâ‚‚ ha).zip (pair bâ‚ bâ‚‚ hb) = pair (aâ‚, bâ‚) (aâ‚‚, bâ‚‚) (Prod.le_def.2 âŸ¨ha, hbâŸ©) := by
  unfold Chain; ext n : 2; cases n <;> rfl

end Chain

end OmegaCompletePartialOrder

open OmegaCompletePartialOrder Chain

/-- An omega-complete partial order is a partial order with a supremum
operation on increasing sequences indexed by natural numbers (which we
call `Ï‰Sup`). In this sense, it is strictly weaker than join complete
semi-lattices as only Ï‰-sized totally ordered sets have a supremum.

See the definition on page 114 of [gunter1992]. -/
class OmegaCompletePartialOrder (Î± : Type*) extends PartialOrder Î± where
  /-- The supremum of an increasing sequence -/
  Ï‰Sup : Chain Î± â†’ Î±
  /-- `Ï‰Sup` is an upper bound of the increasing sequence -/
  le_Ï‰Sup : âˆ€ c : Chain Î±, âˆ€ i, c i â‰¤ Ï‰Sup c
  /-- `Ï‰Sup` is a lower bound of the set of upper bounds of the increasing sequence -/
  Ï‰Sup_le : âˆ€ (c : Chain Î±) (x), (âˆ€ i, c i â‰¤ x) â†’ Ï‰Sup c â‰¤ x

namespace OmegaCompletePartialOrder
variable [OmegaCompletePartialOrder Î±]

/-- Transfer an `OmegaCompletePartialOrder` on `Î²` to an `OmegaCompletePartialOrder` on `Î±`
using a strictly monotone function `f : Î² â†’o Î±`, a definition of Ï‰Sup and a proof that `f` is
continuous with regard to the provided `Ï‰Sup` and the Ï‰CPO on `Î±`. -/
protected abbrev lift [PartialOrder Î²] (f : Î² â†’o Î±) (Ï‰Supâ‚€ : Chain Î² â†’ Î²)
    (h : âˆ€ x y, f x â‰¤ f y â†’ x â‰¤ y) (h' : âˆ€ c, f (Ï‰Supâ‚€ c) = Ï‰Sup (c.map f)) :
    OmegaCompletePartialOrder Î² where
  Ï‰Sup := Ï‰Supâ‚€
  Ï‰Sup_le c x hx := h _ _ (by rw [h']; apply Ï‰Sup_le; intro i; apply f.monotone (hx i))
  le_Ï‰Sup c i := h _ _ (by rw [h']; apply le_Ï‰Sup (c.map f))

theorem le_Ï‰Sup_of_le {c : Chain Î±} {x : Î±} (i : â„•) (h : x â‰¤ c i) : x â‰¤ Ï‰Sup c :=
  le_trans h (le_Ï‰Sup c _)

theorem Ï‰Sup_total {c : Chain Î±} {x : Î±} (h : âˆ€ i, c i â‰¤ x âˆ¨ x â‰¤ c i) : Ï‰Sup c â‰¤ x âˆ¨ x â‰¤ Ï‰Sup c :=
  by_cases
    (fun (this : âˆ€ i, c i â‰¤ x) => Or.inl (Ï‰Sup_le _ _ this))
    (fun (this : Â¬âˆ€ i, c i â‰¤ x) =>
      have : âˆƒ i, Â¬c i â‰¤ x := by simp only [not_forall] at this âŠ¢; assumption
      let âŸ¨i, hxâŸ© := this
      have : x â‰¤ c i := (h i).resolve_left hx
      Or.inr <| le_Ï‰Sup_of_le _ this)

@[mono]
theorem Ï‰Sup_le_Ï‰Sup_of_le {câ‚€ câ‚ : Chain Î±} (h : câ‚€ â‰¤ câ‚) : Ï‰Sup câ‚€ â‰¤ Ï‰Sup câ‚ :=
  (Ï‰Sup_le _ _) fun i => by
    obtain âŸ¨_, hâŸ© := h i
    exact le_trans h (le_Ï‰Sup _ _)

@[simp] theorem Ï‰Sup_le_iff {c : Chain Î±} {x : Î±} : Ï‰Sup c â‰¤ x â†” âˆ€ i, c i â‰¤ x := by
  constructor <;> intros
  Â· trans Ï‰Sup c
    Â· exact le_Ï‰Sup _ _
    Â· assumption
  exact Ï‰Sup_le _ _ â€¹_â€º

lemma isLUB_range_Ï‰Sup (c : Chain Î±) : IsLUB (Set.range c) (Ï‰Sup c) := by
  constructor
  Â· simp only [upperBounds, Set.mem_range, forall_exists_index, forall_apply_eq_imp_iff,
      Set.mem_setOf_eq]
    exact fun a â†¦ le_Ï‰Sup c a
  Â· simp only [lowerBounds, upperBounds, Set.mem_range, forall_exists_index,
      forall_apply_eq_imp_iff, Set.mem_setOf_eq]
    exact fun â¦ƒaâ¦„ a_1 â†¦ Ï‰Sup_le c a a_1

lemma Ï‰Sup_eq_of_isLUB {c : Chain Î±} {a : Î±} (h : IsLUB (Set.range c) a) : a = Ï‰Sup c := by
  rw [le_antisymm_iff]
  simp only [IsLUB, IsLeast, upperBounds, lowerBounds, Set.mem_range, forall_exists_index,
    forall_apply_eq_imp_iff, Set.mem_setOf_eq] at h
  constructor
  Â· apply h.2
    exact fun a â†¦ le_Ï‰Sup c a
  Â· rw [Ï‰Sup_le_iff]
    apply h.1

/-- A subset `p : Î± â†’ Prop` of the type closed under `Ï‰Sup` induces an
`OmegaCompletePartialOrder` on the subtype `{a : Î± // p a}`. -/
def subtype {Î± : Type*} [OmegaCompletePartialOrder Î±] (p : Î± â†’ Prop)
    (hp : âˆ€ c : Chain Î±, (âˆ€ i âˆˆ c, p i) â†’ p (Ï‰Sup c)) : OmegaCompletePartialOrder (Subtype p) :=
  OmegaCompletePartialOrder.lift (OrderHom.Subtype.val p)
    (fun c => âŸ¨Ï‰Sup _, hp (c.map (OrderHom.Subtype.val p)) fun _ âŸ¨n, qâŸ© => q.symm â–¸ (c n).2âŸ©)
    (fun _ _ h => h) (fun _ => rfl)

section Continuity

variable [OmegaCompletePartialOrder Î²]
variable [OmegaCompletePartialOrder Î³]
variable {f : Î± â†’ Î²} {g : Î² â†’ Î³}

/-- A function `f` between `Ï‰`-complete partial orders is `Ï‰ScottContinuous` if it is
Scott continuous over chains. -/
def Ï‰ScottContinuous (f : Î± â†’ Î²) : Prop :=
    ScottContinuousOn (Set.range fun c : Chain Î± => Set.range c) f

lemma _root_.ScottContinuous.Ï‰ScottContinuous (hf : ScottContinuous f) : Ï‰ScottContinuous f :=
  hf.scottContinuousOn

lemma Ï‰ScottContinuous.monotone (h : Ï‰ScottContinuous f) : Monotone f :=
  ScottContinuousOn.monotone _ (fun a b hab => by
    use pair a b hab; exact range_pair a b hab) h

lemma Ï‰ScottContinuous.isLUB {c : Chain Î±} (hf : Ï‰ScottContinuous f) :
    IsLUB (Set.range (c.map âŸ¨f, hf.monotoneâŸ©)) (f (Ï‰Sup c)) := by
  simpa [map_coe, OrderHom.coe_mk, Set.range_comp]
    using hf (by simp) (Set.range_nonempty _) (isChain_range c).directedOn (isLUB_range_Ï‰Sup c)

lemma Ï‰ScottContinuous.id : Ï‰ScottContinuous (id : Î± â†’ Î±) := ScottContinuousOn.id

lemma Ï‰ScottContinuous.map_Ï‰Sup (hf : Ï‰ScottContinuous f) (c : Chain Î±) :
    f (Ï‰Sup c) = Ï‰Sup (c.map âŸ¨f, hf.monotoneâŸ©) := Ï‰Sup_eq_of_isLUB hf.isLUB

/-- `Ï‰ScottContinuous f` asserts that `f` is both monotone and distributes over Ï‰Sup. -/
lemma Ï‰ScottContinuous_iff_monotone_map_Ï‰Sup :
    Ï‰ScottContinuous f â†” âˆƒ hf : Monotone f, âˆ€ c : Chain Î±, f (Ï‰Sup c) = Ï‰Sup (c.map âŸ¨f, hfâŸ©) := by
  refine âŸ¨fun hf â†¦ âŸ¨hf.monotone, hf.map_Ï‰SupâŸ©, ?_âŸ©
  intro hf _ âŸ¨c, hcâŸ© _ _ _ hda
  convert isLUB_range_Ï‰Sup (c.map { toFun := f, monotone' := hf.1 })
  Â· rw [map_coe, OrderHom.coe_mk, â† hc, â† (Set.range_comp f â‡‘c)]
  Â· rw [â† hc] at hda
    rw [â† hf.2 c, Ï‰Sup_eq_of_isLUB hda]

alias âŸ¨Ï‰ScottContinuous.monotone_map_Ï‰Sup, Ï‰ScottContinuous.of_monotone_map_Ï‰SupâŸ© :=
  Ï‰ScottContinuous_iff_monotone_map_Ï‰Sup

/- A monotone function `f : Î± â†’o Î²` is Ï‰Scott continuous if and only if it distributes over Ï‰Sup. -/
lemma Ï‰ScottContinuous_iff_map_Ï‰Sup_of_orderHom {f : Î± â†’o Î²} :
    Ï‰ScottContinuous f â†” âˆ€ c : Chain Î±, f (Ï‰Sup c) = Ï‰Sup (c.map f) := by
  rw [Ï‰ScottContinuous_iff_monotone_map_Ï‰Sup]
  exact exists_prop_of_true f.monotone'

alias âŸ¨Ï‰ScottContinuous.map_Ï‰Sup_of_orderHom, Ï‰ScottContinuous.of_map_Ï‰Sup_of_orderHomâŸ© :=
  Ï‰ScottContinuous_iff_map_Ï‰Sup_of_orderHom

lemma Ï‰ScottContinuous.comp (hg : Ï‰ScottContinuous g) (hf : Ï‰ScottContinuous f) :
    Ï‰ScottContinuous (g.comp f) :=
  Ï‰ScottContinuous.of_monotone_map_Ï‰Sup
    âŸ¨hg.monotone.comp hf.monotone, by simp [hf.map_Ï‰Sup, hg.map_Ï‰Sup, map_comp]âŸ©

lemma Ï‰ScottContinuous.const {x : Î²} : Ï‰ScottContinuous (Function.const Î± x) := by
  simp [Ï‰ScottContinuous, ScottContinuousOn, Set.range_nonempty]

end Continuity

end OmegaCompletePartialOrder

namespace Part

theorem eq_of_chain {c : Chain (Part Î±)} {a b : Î±} (ha : some a âˆˆ c) (hb : some b âˆˆ c) : a = b := by
  obtain âŸ¨i, haâŸ© := ha; replace ha := ha.symm
  obtain âŸ¨j, hbâŸ© := hb; replace hb := hb.symm
  rw [eq_some_iff] at ha hb
  rcases le_total i j with hij | hji
  Â· have := c.monotone hij _ ha; apply mem_unique this hb
  Â· have := c.monotone hji _ hb; apply Eq.symm; apply mem_unique this ha

open Classical in
/-- The (noncomputable) `Ï‰Sup` definition for the `Ï‰`-CPO structure on `Part Î±`. -/
protected noncomputable def Ï‰Sup (c : Chain (Part Î±)) : Part Î± :=
  if h : âˆƒ a, some a âˆˆ c then some (Classical.choose h) else none

theorem Ï‰Sup_eq_some {c : Chain (Part Î±)} {a : Î±} (h : some a âˆˆ c) : Part.Ï‰Sup c = some a :=
  have : âˆƒ a, some a âˆˆ c := âŸ¨a, hâŸ©
  have a' : some (Classical.choose this) âˆˆ c := Classical.choose_spec this
  calc
    Part.Ï‰Sup c = some (Classical.choose this) := dif_pos this
    _ = some a := congr_arg _ (eq_of_chain a' h)

theorem Ï‰Sup_eq_none {c : Chain (Part Î±)} (h : Â¬âˆƒ a, some a âˆˆ c) : Part.Ï‰Sup c = none :=
  dif_neg h

theorem mem_chain_of_mem_Ï‰Sup {c : Chain (Part Î±)} {a : Î±} (h : a âˆˆ Part.Ï‰Sup c) : some a âˆˆ c := by
  simp only [Part.Ï‰Sup] at h; split_ifs at h with h_1
  Â· have h' := Classical.choose_spec h_1
    rw [â† eq_some_iff] at h
    rw [â† h]
    exact h'
  Â· rcases h with âŸ¨âŸ¨âŸ©âŸ©

noncomputable instance omegaCompletePartialOrder :
    OmegaCompletePartialOrder (Part Î±) where
  Ï‰Sup := Part.Ï‰Sup
  le_Ï‰Sup c i := by
    intro x hx
    rw [â† eq_some_iff] at hx âŠ¢
    rw [Ï‰Sup_eq_some]
    rw [â† hx]
    exact âŸ¨i, rflâŸ©
  Ï‰Sup_le := by
    rintro c x hx a ha
    replace ha := mem_chain_of_mem_Ï‰Sup ha
    obtain âŸ¨i, haâŸ© := ha
    apply hx i
    rw [â† ha]
    apply mem_some

section Inst

theorem mem_Ï‰Sup (x : Î±) (c : Chain (Part Î±)) : x âˆˆ Ï‰Sup c â†” some x âˆˆ c := by
  simp only [Ï‰Sup, Part.Ï‰Sup]
  constructor
  Â· split_ifs with h
    swap
    Â· rintro âŸ¨âŸ¨âŸ©âŸ©
    intro h'
    have hh := Classical.choose_spec h
    simp only [mem_some_iff] at h'
    subst x
    exact hh
  Â· intro h
    have h' : âˆƒ a : Î±, some a âˆˆ c := âŸ¨_, hâŸ©
    rw [dif_pos h']
    have hh := Classical.choose_spec h'
    rw [eq_of_chain hh h]
    simp

end Inst

end Part

section Pi

variable {Î² : Î± â†’ Type*}

instance [âˆ€ a, OmegaCompletePartialOrder (Î² a)] :
    OmegaCompletePartialOrder (âˆ€ a, Î² a) where
  Ï‰Sup c a := Ï‰Sup (c.map (Pi.evalOrderHom a))
  Ï‰Sup_le _ _ hf a :=
    Ï‰Sup_le _ _ <| by
      rintro i
      apply hf
  le_Ï‰Sup _ _ _ := le_Ï‰Sup_of_le _ <| le_rfl

namespace OmegaCompletePartialOrder

variable [âˆ€ x, OmegaCompletePartialOrder <| Î² x]
variable [OmegaCompletePartialOrder Î³]
variable {f : Î³ â†’ âˆ€ x, Î² x}

lemma Ï‰ScottContinuous.applyâ‚‚ (hf : Ï‰ScottContinuous f) (a : Î±) : Ï‰ScottContinuous (f Â· a) :=
  Ï‰ScottContinuous.of_monotone_map_Ï‰Sup
    âŸ¨fun _ _ h â†¦ hf.monotone h a, fun c â†¦ congr_fun (hf.map_Ï‰Sup c) aâŸ©

lemma Ï‰ScottContinuous.of_applyâ‚‚ (hf : âˆ€ a, Ï‰ScottContinuous (f Â· a)) : Ï‰ScottContinuous f :=
  Ï‰ScottContinuous.of_monotone_map_Ï‰Sup
    âŸ¨fun _ _ h a â†¦ (hf a).monotone h, fun c â†¦ by ext a; apply (hf a).map_Ï‰Sup câŸ©

lemma Ï‰ScottContinuous_iff_applyâ‚‚ : Ï‰ScottContinuous f â†” âˆ€ a, Ï‰ScottContinuous (f Â· a) :=
  âŸ¨Ï‰ScottContinuous.applyâ‚‚, Ï‰ScottContinuous.of_applyâ‚‚âŸ©

end OmegaCompletePartialOrder

end Pi

namespace Prod

variable [OmegaCompletePartialOrder Î±]
variable [OmegaCompletePartialOrder Î²]
variable [OmegaCompletePartialOrder Î³]

/-- The supremum of a chain in the product `Ï‰`-CPO. -/
@[simps]
protected def Ï‰SupImpl (c : Chain (Î± Ã— Î²)) : Î± Ã— Î² :=
  (Ï‰Sup (c.map OrderHom.fst), Ï‰Sup (c.map OrderHom.snd))

@[simps! Ï‰Sup_fst Ï‰Sup_snd]
instance : OmegaCompletePartialOrder (Î± Ã— Î²) where
  Ï‰Sup := Prod.Ï‰SupImpl
  Ï‰Sup_le := fun _ _ h => âŸ¨Ï‰Sup_le _ _ fun i => (h i).1, Ï‰Sup_le _ _ fun i => (h i).2âŸ©
  le_Ï‰Sup c i := âŸ¨le_Ï‰Sup (c.map OrderHom.fst) i, le_Ï‰Sup (c.map OrderHom.snd) iâŸ©

theorem Ï‰Sup_zip (câ‚€ : Chain Î±) (câ‚ : Chain Î²) : Ï‰Sup (câ‚€.zip câ‚) = (Ï‰Sup câ‚€, Ï‰Sup câ‚) := rfl

end Prod

namespace CompleteLattice

-- see Note [lower instance priority]
/-- Any complete lattice has an `Ï‰`-CPO structure where the countable supremum is a special case
of arbitrary suprema. -/
instance (priority := 100) [CompleteLattice Î±] : OmegaCompletePartialOrder Î± where
  Ï‰Sup c := â¨† i, c i
  Ï‰Sup_le := fun âŸ¨c, _âŸ© s hs => by simpa only [iSup_le_iff]
  le_Ï‰Sup := fun âŸ¨c, _âŸ© i => le_iSup_of_le i le_rfl

variable [OmegaCompletePartialOrder Î±] [CompleteLattice Î²] {f g : Î± â†’ Î²}

-- TODO Prove this result for `ScottContinuousOn` and deduce this as a special case
-- https://github.com/leanprover-community/mathlib4/pull/15412
open Chain in
lemma Ï‰ScottContinuous.prodMk (hf : Ï‰ScottContinuous f) (hg : Ï‰ScottContinuous g) :
    Ï‰ScottContinuous fun x => (f x, g x) := ScottContinuousOn.prodMk (fun a b hab => by
  use pair a b hab; exact range_pair a b hab) hf hg

lemma Ï‰ScottContinuous.iSup {f : Î¹ â†’ Î± â†’ Î²} (hf : âˆ€ i, Ï‰ScottContinuous (f i)) :
    Ï‰ScottContinuous (â¨† i, f i) := by
  refine Ï‰ScottContinuous.of_monotone_map_Ï‰Sup
    âŸ¨Monotone.iSup fun i â†¦ (hf i).monotone, fun c â†¦ eq_of_forall_ge_iff fun a â†¦ ?_âŸ©
  simp +contextual [Ï‰Sup_le_iff, (hf _).map_Ï‰Sup, @forall_swap Î¹]

lemma Ï‰ScottContinuous.sSup {s : Set (Î± â†’ Î²)} (hs : âˆ€ f âˆˆ s, Ï‰ScottContinuous f) :
    Ï‰ScottContinuous (sSup s) := by
  rw [sSup_eq_iSup]; exact Ï‰ScottContinuous.iSup fun f â†¦ Ï‰ScottContinuous.iSup <| hs f

lemma Ï‰ScottContinuous.sup (hf : Ï‰ScottContinuous f) (hg : Ï‰ScottContinuous g) :
    Ï‰ScottContinuous (f âŠ” g) := by
  rw [â† sSup_pair]
  apply Ï‰ScottContinuous.sSup
  rintro f (rfl | rfl | _) <;> assumption

lemma Ï‰ScottContinuous.top : Ï‰ScottContinuous (âŠ¤ : Î± â†’ Î²) :=
  Ï‰ScottContinuous.of_monotone_map_Ï‰Sup
    âŸ¨monotone_const, fun c â†¦ eq_of_forall_ge_iff fun a â†¦ by simpâŸ©

lemma Ï‰ScottContinuous.bot : Ï‰ScottContinuous (âŠ¥ : Î± â†’ Î²) := by
  rw [â† sSup_empty]; exact Ï‰ScottContinuous.sSup (by simp)

end CompleteLattice

namespace CompleteLattice

variable [OmegaCompletePartialOrder Î±] [CompleteLinearOrder Î²] {f g : Î± â†’ Î²}

-- TODO Prove this result for `ScottContinuousOn` and deduce this as a special case
-- Also consider if it holds in greater generality (e.g. finite sets)
-- N.B. The Scott Topology coincides with the Upper Topology on a Complete Linear Order
-- `Topology.IsScott.scott_eq_upper_of_completeLinearOrder`
-- We have that the product topology coincides with the upper topology
-- https://github.com/leanprover-community/mathlib4/pull/12133
lemma Ï‰ScottContinuous.inf (hf : Ï‰ScottContinuous f) (hg : Ï‰ScottContinuous g) :
    Ï‰ScottContinuous (f âŠ“ g) := by
  refine Ï‰ScottContinuous.of_monotone_map_Ï‰Sup
    âŸ¨hf.monotone.inf hg.monotone, fun c â†¦ eq_of_forall_ge_iff fun a â†¦ ?_âŸ©
  simp only [Pi.inf_apply, hf.map_Ï‰Sup c, hg.map_Ï‰Sup c, inf_le_iff, Ï‰Sup_le_iff, Chain.map_coe,
    Function.comp, OrderHom.coe_mk, â† forall_or_left, â† forall_or_right]
  exact âŸ¨fun h _ â†¦ h _ _, fun h i j â†¦
    (h (max j i)).imp (le_trans <| hf.monotone <| c.mono <| le_max_left _ _)
      (le_trans <| hg.monotone <| c.mono <| le_max_right _ _)âŸ©

end CompleteLattice

namespace OmegaCompletePartialOrder
variable [OmegaCompletePartialOrder Î±] [OmegaCompletePartialOrder Î²]
variable [OmegaCompletePartialOrder Î³] [OmegaCompletePartialOrder Î´]

namespace OrderHom

/-- The `Ï‰Sup` operator for monotone functions. -/
@[simps]
protected def Ï‰Sup (c : Chain (Î± â†’o Î²)) : Î± â†’o Î² where
  toFun a := Ï‰Sup (c.map (OrderHom.apply a))
  monotone' _ _ h := Ï‰Sup_le_Ï‰Sup_of_le ((Chain.map_le_map _) fun a => a.monotone h)

@[simps! Ï‰Sup_coe]
instance omegaCompletePartialOrder : OmegaCompletePartialOrder (Î± â†’o Î²) :=
  OmegaCompletePartialOrder.lift OrderHom.coeFnHom OrderHom.Ï‰Sup (fun _ _ h => h) fun _ => rfl

end OrderHom

variable (Î± Î²) in
/-- A monotone function on `Ï‰`-continuous partial orders is said to be continuous
if for every chain `c : chain Î±`, `f (âŠ” i, c i) = âŠ” i, f (c i)`.
This is just the bundled version of `OrderHom.continuous`. -/
structure ContinuousHom extends OrderHom Î± Î² where
  /-- The underlying function of a `ContinuousHom` is continuous, i.e. it preserves `Ï‰Sup` -/
  protected map_Ï‰Sup' (c : Chain Î±) : toFun (Ï‰Sup c) = Ï‰Sup (c.map toOrderHom)

attribute [nolint docBlame] ContinuousHom.toOrderHom

@[inherit_doc] infixr:25 " â†’ğ’„ " => ContinuousHom -- Input: \r\MIc

instance : FunLike (Î± â†’ğ’„ Î²) Î± Î² where
  coe f := f.toFun
  coe_injective' := by rintro âŸ¨âŸ© âŸ¨âŸ© h; congr; exact DFunLike.ext' h

instance : OrderHomClass (Î± â†’ğ’„ Î²) Î± Î² where
  map_rel f _ _ h := f.mono h

instance : PartialOrder (Î± â†’ğ’„ Î²) :=
  (PartialOrder.lift fun f => f.toOrderHom.toFun) <| by rintro âŸ¨âŸ¨âŸ©âŸ© âŸ¨âŸ¨âŸ©âŸ© h; congr

namespace ContinuousHom

protected lemma Ï‰ScottContinuous (f : Î± â†’ğ’„ Î²) : Ï‰ScottContinuous f :=
  Ï‰ScottContinuous.of_map_Ï‰Sup_of_orderHom f.map_Ï‰Sup'

-- Not a `simp` lemma because in many cases projection is simpler than a generic coercion
theorem toOrderHom_eq_coe (f : Î± â†’ğ’„ Î²) : f.1 = f := rfl

@[simp] theorem coe_mk (f : Î± â†’o Î²) (hf) : â‡‘(mk f hf) = f := rfl

@[simp] theorem coe_toOrderHom (f : Î± â†’ğ’„ Î²) : â‡‘f.1 = f := rfl

/-- See Note [custom simps projection]. We specify this explicitly because we don't have a DFunLike
instance.
-/
def Simps.apply (h : Î± â†’ğ’„ Î²) : Î± â†’ Î² :=
  h

initialize_simps_projections ContinuousHom (toFun â†’ apply)

protected theorem congr_fun {f g : Î± â†’ğ’„ Î²} (h : f = g) (x : Î±) : f x = g x :=
  DFunLike.congr_fun h x

protected theorem congr_arg (f : Î± â†’ğ’„ Î²) {x y : Î±} (h : x = y) : f x = f y :=
  congr_arg f h

protected theorem monotone (f : Î± â†’ğ’„ Î²) : Monotone f :=
  f.monotone'

@[mono]
theorem apply_mono {f g : Î± â†’ğ’„ Î²} {x y : Î±} (hâ‚ : f â‰¤ g) (hâ‚‚ : x â‰¤ y) : f x â‰¤ g y :=
  OrderHom.apply_mono (show (f : Î± â†’o Î²) â‰¤ g from hâ‚) hâ‚‚

theorem Ï‰Sup_bind {Î² Î³ : Type v} (c : Chain Î±) (f : Î± â†’o Part Î²) (g : Î± â†’o Î² â†’ Part Î³) :
    Ï‰Sup (c.map (f.partBind g)) = Ï‰Sup (c.map f) >>= Ï‰Sup (c.map g) := by
  apply eq_of_forall_ge_iff; intro x
  simp only [Ï‰Sup_le_iff, Part.bind_le]
  constructor <;> intro h'''
  Â· intro b hb
    apply Ï‰Sup_le _ _ _
    rintro i y hy
    simp only [Part.mem_Ï‰Sup] at hb
    rcases hb with âŸ¨j, hbâŸ©
    replace hb := hb.symm
    simp only [Part.eq_some_iff, Chain.map_coe, Function.comp_apply] at hy hb
    replace hb : b âˆˆ f (c (max i j)) := f.mono (c.mono (le_max_right i j)) _ hb
    replace hy : y âˆˆ g (c (max i j)) b := g.mono (c.mono (le_max_left i j)) _ _ hy
    apply h''' (max i j)
    simp only [Part.mem_bind_iff, Chain.map_coe,
      Function.comp_apply, OrderHom.partBind_coe]
    exact âŸ¨_, hb, hyâŸ©
  Â· intro i y hy
    simp only [Part.mem_bind_iff, Chain.map_coe,
      Function.comp_apply, OrderHom.partBind_coe] at hy
    rcases hy with âŸ¨b, hbâ‚€, hbâ‚âŸ©
    apply h''' b _
    Â· apply le_Ï‰Sup (c.map g) _ _ _ hbâ‚
    Â· apply le_Ï‰Sup (c.map f) i _ hbâ‚€

-- TODO: We should move `Ï‰ScottContinuous` to the root namespace
lemma Ï‰ScottContinuous.bind {Î² Î³} {f : Î± â†’ Part Î²} {g : Î± â†’ Î² â†’ Part Î³} (hf : Ï‰ScottContinuous f)
    (hg : Ï‰ScottContinuous g) : Ï‰ScottContinuous fun x â†¦ f x >>= g x :=
  Ï‰ScottContinuous.of_monotone_map_Ï‰Sup
    âŸ¨hf.monotone.partBind hg.monotone, fun c â†¦ by rw [hf.map_Ï‰Sup, hg.map_Ï‰Sup, â† Ï‰Sup_bind]; rflâŸ©

lemma Ï‰ScottContinuous.map {Î² Î³} {f : Î² â†’ Î³} {g : Î± â†’ Part Î²} (hg : Ï‰ScottContinuous g) :
    Ï‰ScottContinuous fun x â†¦ f <$> g x := by
  simpa only [map_eq_bind_pure_comp] using Ï‰ScottContinuous.bind hg Ï‰ScottContinuous.const

lemma Ï‰ScottContinuous.seq {Î² Î³} {f : Î± â†’ Part (Î² â†’ Î³)} {g : Î± â†’ Part Î²} (hf : Ï‰ScottContinuous f)
    (hg : Ï‰ScottContinuous g) : Ï‰ScottContinuous fun x â†¦ f x <*> g x := by
  simp only [seq_eq_bind_map]
  exact Ï‰ScottContinuous.bind hf <| Ï‰ScottContinuous.of_applyâ‚‚ fun _ â†¦ Ï‰ScottContinuous.map hg

theorem continuous (F : Î± â†’ğ’„ Î²) (C : Chain Î±) : F (Ï‰Sup C) = Ï‰Sup (C.map F) :=
  F.Ï‰ScottContinuous.map_Ï‰Sup _

/-- Construct a continuous function from a bare function, a continuous function, and a proof that
they are equal. -/
@[simps!]
def copy (f : Î± â†’ Î²) (g : Î± â†’ğ’„ Î²) (h : f = g) : Î± â†’ğ’„ Î² where
  toOrderHom := g.1.copy f h
  map_Ï‰Sup' := by rw [OrderHom.copy_eq]; exact g.map_Ï‰Sup'

/-- The identity as a continuous function. -/
@[simps!]
def id : Î± â†’ğ’„ Î± := âŸ¨OrderHom.id, Ï‰ScottContinuous.id.map_Ï‰SupâŸ©

/-- The composition of continuous functions. -/
@[simps!]
def comp (f : Î² â†’ğ’„ Î³) (g : Î± â†’ğ’„ Î²) : Î± â†’ğ’„ Î³ :=
  âŸ¨.comp f.1 g.1, (f.Ï‰ScottContinuous.comp g.Ï‰ScottContinuous).map_Ï‰SupâŸ©

@[ext]
protected theorem ext (f g : Î± â†’ğ’„ Î²) (h : âˆ€ x, f x = g x) : f = g := DFunLike.ext f g h

protected theorem coe_inj (f g : Î± â†’ğ’„ Î²) (h : (f : Î± â†’ Î²) = g) : f = g :=
  DFunLike.ext' h

@[simp]
theorem comp_id (f : Î² â†’ğ’„ Î³) : f.comp id = f := rfl

@[simp]
theorem id_comp (f : Î² â†’ğ’„ Î³) : id.comp f = f := rfl

@[simp]
theorem comp_assoc (f : Î³ â†’ğ’„ Î´) (g : Î² â†’ğ’„ Î³) (h : Î± â†’ğ’„ Î²) : f.comp (g.comp h) = (f.comp g).comp h :=
  rfl

@[simp]
theorem coe_apply (a : Î±) (f : Î± â†’ğ’„ Î²) : (f : Î± â†’o Î²) a = f a :=
  rfl

/-- `Function.const` is a continuous function. -/
@[simps!]
def const (x : Î²) : Î± â†’ğ’„ Î² := âŸ¨.const _ x, Ï‰ScottContinuous.const.map_Ï‰SupâŸ©

instance [Inhabited Î²] : Inhabited (Î± â†’ğ’„ Î²) :=
  âŸ¨const defaultâŸ©

/-- The map from continuous functions to monotone functions is itself a monotone function. -/
@[simps]
def toMono : (Î± â†’ğ’„ Î²) â†’o Î± â†’o Î² where
  toFun f := f
  monotone' _ _ h := h

/-- When proving that a chain of applications is below a bound `z`, it suffices to consider the
functions and values being selected from the same index in the chains.

This lemma is more specific than necessary, i.e. `câ‚€` only needs to be a
chain of monotone functions, but it is only used with continuous functions. -/
@[simp]
theorem forall_forall_merge (câ‚€ : Chain (Î± â†’ğ’„ Î²)) (câ‚ : Chain Î±) (z : Î²) :
    (âˆ€ i j : â„•, (câ‚€ i) (câ‚ j) â‰¤ z) â†” âˆ€ i : â„•, (câ‚€ i) (câ‚ i) â‰¤ z := by
  constructor <;> introv h
  Â· apply h
  Â· apply le_trans _ (h (max i j))
    trans câ‚€ i (câ‚ (max i j))
    Â· apply (câ‚€ i).monotone
      apply câ‚.monotone
      apply le_max_right
    Â· apply câ‚€.monotone
      apply le_max_left

@[simp]
theorem forall_forall_merge' (câ‚€ : Chain (Î± â†’ğ’„ Î²)) (câ‚ : Chain Î±) (z : Î²) :
    (âˆ€ j i : â„•, (câ‚€ i) (câ‚ j) â‰¤ z) â†” âˆ€ i : â„•, (câ‚€ i) (câ‚ i) â‰¤ z := by
  rw [forall_swap, forall_forall_merge]

/-- The `Ï‰Sup` operator for continuous functions, which takes the pointwise countable supremum
of the functions in the `Ï‰`-chain. -/
@[simps!]
protected def Ï‰Sup (c : Chain (Î± â†’ğ’„ Î²)) : Î± â†’ğ’„ Î² where
  toOrderHom := Ï‰Sup <| c.map toMono
  map_Ï‰Sup' c' := eq_of_forall_ge_iff fun a â†¦ by simp [(c _).Ï‰ScottContinuous.map_Ï‰Sup]

@[simps Ï‰Sup]
instance : OmegaCompletePartialOrder (Î± â†’ğ’„ Î²) :=
  OmegaCompletePartialOrder.lift ContinuousHom.toMono ContinuousHom.Ï‰Sup
    (fun _ _ h => h) (fun _ => rfl)

namespace Prod

/-- The application of continuous functions as a continuous function. -/
@[simps]
def apply : (Î± â†’ğ’„ Î²) Ã— Î± â†’ğ’„ Î² where
  toFun f := f.1 f.2
  monotone' x y h := by
    dsimp
    trans y.fst x.snd <;> [apply h.1; apply y.1.monotone h.2]
  map_Ï‰Sup' c := by
    apply le_antisymm
    Â· apply Ï‰Sup_le
      intro i
      dsimp
      rw [(c _).fst.continuous]
      apply Ï‰Sup_le
      intro j
      apply le_Ï‰Sup_of_le (max i j)
      apply apply_mono
      Â· exact monotone_fst (OrderHom.mono _ (le_max_left _ _))
      Â· exact monotone_snd (OrderHom.mono _ (le_max_right _ _))
    Â· apply Ï‰Sup_le
      intro i
      apply le_Ï‰Sup_of_le i
      dsimp
      apply OrderHom.mono _
      apply le_Ï‰Sup_of_le i
      rfl

end Prod

theorem Ï‰Sup_apply_Ï‰Sup (câ‚€ : Chain (Î± â†’ğ’„ Î²)) (câ‚ : Chain Î±) :
    Ï‰Sup câ‚€ (Ï‰Sup câ‚) = Prod.apply (Ï‰Sup (câ‚€.zip câ‚)) := by simp [Prod.apply_apply, Prod.Ï‰Sup_zip]

/-- A family of continuous functions yields a continuous family of functions. -/
@[simps]
def flip {Î± : Type*} (f : Î± â†’ Î² â†’ğ’„ Î³) : Î² â†’ğ’„ Î± â†’ Î³ where
  toFun x y := f y x
  monotone' _ _ h a := (f a).monotone h
  map_Ï‰Sup' _ := by ext x; change f _ _ = _; rw [(f _).continuous]; rfl

/-- `Part.bind` as a continuous function. -/
@[simps! apply]
noncomputable def bind {Î² Î³ : Type v} (f : Î± â†’ğ’„ Part Î²) (g : Î± â†’ğ’„ Î² â†’ Part Î³) : Î± â†’ğ’„ Part Î³ :=
  .mk (OrderHom.partBind f g.toOrderHom) fun c => by
    rw [Ï‰Sup_bind, â† f.continuous, g.toOrderHom_eq_coe, â† g.continuous]
    rfl

/-- `Part.map` as a continuous function. -/
@[simps! apply]
noncomputable def map {Î² Î³ : Type v} (f : Î² â†’ Î³) (g : Î± â†’ğ’„ Part Î²) : Î± â†’ğ’„ Part Î³ :=
  .copy (fun x => f <$> g x) (bind g (const (pure âˆ˜ f))) <| by
    ext1
    simp only [map_eq_bind_pure_comp, bind, coe_mk, OrderHom.partBind_coe, coe_apply,
      coe_toOrderHom, const_apply, Part.bind_eq_bind]

/-- `Part.seq` as a continuous function. -/
@[simps! apply]
noncomputable def seq {Î² Î³ : Type v} (f : Î± â†’ğ’„ Part (Î² â†’ Î³)) (g : Î± â†’ğ’„ Part Î²) : Î± â†’ğ’„ Part Î³ :=
  .copy (fun x => f x <*> g x) (bind f <| flip <| _root_.flip map g) <| by
      ext
      simp only [seq_eq_bind_map, Part.bind_eq_bind, Part.mem_bind_iff, flip_apply, _root_.flip,
        map_apply, bind_apply, Part.map_eq_map]

end ContinuousHom

namespace fixedPoints

open Function

/-- Iteration of a function on an initial element interpreted as a chain. -/
def iterateChain (f : Î± â†’o Î±) (x : Î±) (h : x â‰¤ f x) : Chain Î± :=
  âŸ¨fun n => f^[n] x, f.monotone.monotone_iterate_of_le_map hâŸ©

variable (f : Î± â†’ğ’„ Î±) (x : Î±)

/-- The supremum of iterating a function on x arbitrary often is a fixed point -/
theorem Ï‰Sup_iterate_mem_fixedPoint (h : x â‰¤ f x) :
    Ï‰Sup (iterateChain f x h) âˆˆ fixedPoints f := by
  rw [mem_fixedPoints, IsFixedPt, f.continuous]
  apply le_antisymm
  Â· apply Ï‰Sup_le
    intro n
    simp only [Chain.map_coe, OrderHomClass.coe_coe, comp_apply]
    have : iterateChain f x h (n.succ) = f (iterateChain f x h n) :=
      Function.iterate_succ_apply' ..
    rw [â† this]
    apply le_Ï‰Sup
  Â· apply Ï‰Sup_le
    rintro (_ | n)
    Â· apply le_trans h
      change ((iterateChain f x h).map f) 0 â‰¤ Ï‰Sup ((iterateChain f x h).map (f : Î± â†’o Î±))
      apply le_Ï‰Sup
    Â· have : iterateChain f x h (n.succ) = (iterateChain f x h).map f n :=
        Function.iterate_succ_apply' ..
      rw [this]
      apply le_Ï‰Sup

/-- The supremum of iterating a function on x arbitrary often is smaller than any prefixed point.

A prefixed point is a value `a` with `f a â‰¤ a`. -/
theorem Ï‰Sup_iterate_le_prefixedPoint (h : x â‰¤ f x) {a : Î±}
    (h_a : f a â‰¤ a) (h_x_le_a : x â‰¤ a) :
    Ï‰Sup (iterateChain f x h) â‰¤ a := by
  apply Ï‰Sup_le
  intro n
  induction n with
  | zero => exact h_x_le_a
  | succ n h_ind =>
    have : iterateChain f x h (n.succ) = f (iterateChain f x h n) :=
      Function.iterate_succ_apply' ..
    rw [this]
    exact le_trans (f.monotone h_ind) h_a

/-- The supremum of iterating a function on x arbitrary often is smaller than any fixed point. -/
theorem Ï‰Sup_iterate_le_fixedPoint (h : x â‰¤ f x) {a : Î±}
    (h_a : a âˆˆ fixedPoints f) (h_x_le_a : x â‰¤ a) :
    Ï‰Sup (iterateChain f x h) â‰¤ a := by
  rw [mem_fixedPoints] at h_a
  obtain h_a := Eq.le h_a
  exact Ï‰Sup_iterate_le_prefixedPoint f x h h_a h_x_le_a

end fixedPoints

end OmegaCompletePartialOrder
