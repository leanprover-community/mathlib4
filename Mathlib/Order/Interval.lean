/-
Copyright (c) 2022 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies
-/
import Mathlib.Data.Set.Intervals.Basic
import Mathlib.Data.Set.Lattice
import Mathlib.Data.SetLike.Basic
import Mathlib.Init.Data.Prod

#align_import order.interval from "leanprover-community/mathlib"@"6623e6af705e97002a9054c1c05a980180276fc1"

/-!
# Order intervals

This file defines (nonempty) closed intervals in an order (see `Set.Icc`). This is a prototype for
interval arithmetic.

## Main declarations

* `NonemptyInterval`: Nonempty intervals. Pairs where the second element is greater than the first.
* `Interval`: Intervals. Either `‚àÖ` or a nonempty interval.
-/


open Function OrderDual Set

variable {Œ± Œ≤ Œ≥ Œ¥ : Type*} {Œπ : Sort*} {Œ∫ : Œπ ‚Üí Sort*}

/-- The nonempty closed intervals in an order.

We define intervals by the pair of endpoints `fst`, `snd`. To convert intervals to the set of
elements between these endpoints, use the coercion `NonemptyInterval Œ± ‚Üí Set Œ±`. -/
-- @[ext] -- porting note: generates the wrong lemma
-- in lean3 it generated `x.toProd = y.toProd ‚Üí x = y`, now it generates
-- `(x.toProd.fst = y.toProd.fst) ‚Üí (x.toProd.snd = y.toProd.snd) ‚Üí x = y`.
-- this is because in `Std.Tactic.Ext.withExtHyps`, the for-loop goes over
-- `getStructureFieldsFlattened` instead of `getStructureFields`.
structure NonemptyInterval (Œ± : Type*) [LE Œ±] extends Prod Œ± Œ± where
  /-- The starting point of an interval is smaller than the endpoint. -/
  fst_le_snd : fst ‚â§ snd
#align nonempty_interval NonemptyInterval

namespace NonemptyInterval

section LE

variable [LE Œ±] {s t : NonemptyInterval Œ±}

theorem toProd_injective : Injective (toProd : NonemptyInterval Œ± ‚Üí Œ± √ó Œ±) := fun s t h =>
  by cases s; cases t; congr
     -- ‚ä¢ { toProd := toProd‚úù, fst_le_snd := fst_le_snd‚úù } = t
              -- ‚ä¢ { toProd := toProd‚úù¬π, fst_le_snd := fst_le_snd‚úù¬π } = { toProd := toProd‚úù, fs ‚Ä¶
                       -- üéâ no goals
#align nonempty_interval.to_prod_injective NonemptyInterval.toProd_injective

-- porting note: This is the manually written old ext-lemma as it was generated in mathlib3.
-- Would be nice to fix `@[ext]` to generate them automatically.
theorem ext (s t : NonemptyInterval Œ±) (h : s.toProd = t.toProd) : s = t := toProd_injective h
#align nonempty_interval.ext NonemptyInterval.ext

-- porting note: This is the manually written old ext-lemma as it was generated in mathlib3.
-- Would be nice to fix `@[ext]` to generate them automatically.
theorem ext_iff (s t : NonemptyInterval Œ±) : s = t ‚Üî s.toProd = t.toProd :=
  toProd_injective.eq_iff.symm
#align nonempty_interval.ext_iff NonemptyInterval.ext_iff

/-- The injection that induces the order on intervals. -/
def toDualProd : NonemptyInterval Œ± ‚Üí Œ±·µí·µà √ó Œ± :=
  toProd
#align nonempty_interval.to_dual_prod NonemptyInterval.toDualProd

@[simp]
theorem toDualProd_apply (s : NonemptyInterval Œ±) : s.toDualProd = (toDual s.fst, s.snd) :=
  Prod.mk.eta.symm
#align nonempty_interval.to_dual_prod_apply NonemptyInterval.toDualProd_apply

theorem toDualProd_injective : Injective (toDualProd : NonemptyInterval Œ± ‚Üí Œ±·µí·µà √ó Œ±) :=
  toProd_injective
#align nonempty_interval.to_dual_prod_injective NonemptyInterval.toDualProd_injective

instance [IsEmpty Œ±] : IsEmpty (NonemptyInterval Œ±) :=
  ‚ü®fun s => isEmptyElim s.fst‚ü©

instance [Subsingleton Œ±] : Subsingleton (NonemptyInterval Œ±) :=
  toDualProd_injective.subsingleton

instance le : LE (NonemptyInterval Œ±) :=
  ‚ü®fun s t => t.fst ‚â§ s.fst ‚àß s.snd ‚â§ t.snd‚ü©

theorem le_def : s ‚â§ t ‚Üî t.fst ‚â§ s.fst ‚àß s.snd ‚â§ t.snd :=
  Iff.rfl
#align nonempty_interval.le_def NonemptyInterval.le_def

/-- `toDualProd` as an order embedding. -/
@[simps]
def toDualProdHom : NonemptyInterval Œ± ‚Ü™o Œ±·µí·µà √ó Œ± where
  toFun := toDualProd
  inj' := toDualProd_injective
  map_rel_iff' := Iff.rfl
#align nonempty_interval.to_dual_prod_hom NonemptyInterval.toDualProdHom

/-- Turn an interval into an interval in the dual order. -/
def dual : NonemptyInterval Œ± ‚âÉ NonemptyInterval Œ±·µí·µà where
  toFun s := ‚ü®s.toProd.swap, s.fst_le_snd‚ü©
  invFun s := ‚ü®s.toProd.swap, s.fst_le_snd‚ü©
  left_inv _ := rfl
  right_inv _ := rfl
#align nonempty_interval.dual NonemptyInterval.dual

@[simp]
theorem fst_dual (s : NonemptyInterval Œ±) : s.dual.fst = toDual s.snd :=
  rfl
#align nonempty_interval.fst_dual NonemptyInterval.fst_dual

@[simp]
theorem snd_dual (s : NonemptyInterval Œ±) : s.dual.snd = toDual s.fst :=
  rfl
#align nonempty_interval.snd_dual NonemptyInterval.snd_dual

end LE

section Preorder

variable [Preorder Œ±] [Preorder Œ≤] [Preorder Œ≥] [Preorder Œ¥] {s : NonemptyInterval Œ±} {x : Œ± √ó Œ±}
  {a : Œ±}

instance : Preorder (NonemptyInterval Œ±) :=
  Preorder.lift toDualProd

instance : Coe (NonemptyInterval Œ±) (Set Œ±) :=
  ‚ü®fun s => Icc s.fst s.snd‚ü©

instance (priority := 100) : Membership Œ± (NonemptyInterval Œ±) :=
  ‚ü®fun a s => a ‚àà (s : Set Œ±)‚ü©

@[simp]
theorem mem_mk {hx : x.1 ‚â§ x.2} : a ‚àà mk x hx ‚Üî x.1 ‚â§ a ‚àß a ‚â§ x.2 :=
  Iff.rfl
#align nonempty_interval.mem_mk NonemptyInterval.mem_mk

theorem mem_def : a ‚àà s ‚Üî s.fst ‚â§ a ‚àß a ‚â§ s.snd :=
  Iff.rfl
#align nonempty_interval.mem_def NonemptyInterval.mem_def

-- @[simp] -- Porting note: not in simpNF
theorem coe_nonempty (s : NonemptyInterval Œ±) : (s : Set Œ±).Nonempty :=
  nonempty_Icc.2 s.fst_le_snd
#align nonempty_interval.coe_nonempty NonemptyInterval.coe_nonempty

/-- `{a}` as an interval. -/
@[simps]
def pure (a : Œ±) : NonemptyInterval Œ± :=
  ‚ü®‚ü®a, a‚ü©, le_rfl‚ü©
#align nonempty_interval.pure NonemptyInterval.pure

theorem mem_pure_self (a : Œ±) : a ‚àà pure a :=
  ‚ü®le_rfl, le_rfl‚ü©
#align nonempty_interval.mem_pure_self NonemptyInterval.mem_pure_self

theorem pure_injective : Injective (pure : Œ± ‚Üí NonemptyInterval Œ±) := fun _ _ =>
  congr_arg <| Prod.fst ‚àò toProd
#align nonempty_interval.pure_injective NonemptyInterval.pure_injective

@[simp]
theorem dual_pure (a : Œ±) : dual (pure a) = pure (toDual a) :=
  rfl
#align nonempty_interval.dual_pure NonemptyInterval.dual_pure

instance [Inhabited Œ±] : Inhabited (NonemptyInterval Œ±) :=
  ‚ü®pure default‚ü©

instance [Nonempty Œ±] : Nonempty (NonemptyInterval Œ±) :=
  Nonempty.map pure (by infer_instance)
                        -- üéâ no goals

instance [Nontrivial Œ±] : Nontrivial (NonemptyInterval Œ±) :=
  pure_injective.nontrivial

/-- Pushforward of nonempty intervals. -/
@[simps!]
def map (f : Œ± ‚Üío Œ≤) (a : NonemptyInterval Œ±) : NonemptyInterval Œ≤ :=
  ‚ü®a.toProd.map f f, f.mono a.fst_le_snd‚ü©
#align nonempty_interval.map NonemptyInterval.map

@[simp]
theorem map_pure (f : Œ± ‚Üío Œ≤) (a : Œ±) : (pure a).map f = pure (f a) :=
  rfl
#align nonempty_interval.map_pure NonemptyInterval.map_pure

@[simp]
theorem map_map (g : Œ≤ ‚Üío Œ≥) (f : Œ± ‚Üío Œ≤) (a : NonemptyInterval Œ±) :
    (a.map f).map g = a.map (g.comp f) :=
  rfl
#align nonempty_interval.map_map NonemptyInterval.map_map

@[simp]
theorem dual_map (f : Œ± ‚Üío Œ≤) (a : NonemptyInterval Œ±) :
    dual (a.map f) = a.dual.map (OrderHom.dual f) :=
  rfl
#align nonempty_interval.dual_map NonemptyInterval.dual_map

/-- Binary pushforward of nonempty intervals. -/
@[simps]
def map‚ÇÇ (f : Œ± ‚Üí Œ≤ ‚Üí Œ≥) (h‚ÇÄ : ‚àÄ b, Monotone fun a => f a b) (h‚ÇÅ : ‚àÄ a, Monotone (f a)) :
    NonemptyInterval Œ± ‚Üí NonemptyInterval Œ≤ ‚Üí NonemptyInterval Œ≥ := fun s t =>
  ‚ü®(f s.fst t.fst, f s.snd t.snd), (h‚ÇÄ _ s.fst_le_snd).trans <| h‚ÇÅ _ t.fst_le_snd‚ü©
#align nonempty_interval.map‚ÇÇ NonemptyInterval.map‚ÇÇ

@[simp]
theorem map‚ÇÇ_pure (f : Œ± ‚Üí Œ≤ ‚Üí Œ≥) (h‚ÇÄ h‚ÇÅ) (a : Œ±) (b : Œ≤) :
    map‚ÇÇ f h‚ÇÄ h‚ÇÅ (pure a) (pure b) = pure (f a b) :=
  rfl
#align nonempty_interval.map‚ÇÇ_pure NonemptyInterval.map‚ÇÇ_pure

@[simp]
theorem dual_map‚ÇÇ (f : Œ± ‚Üí Œ≤ ‚Üí Œ≥) (h‚ÇÄ h‚ÇÅ s t) :
    dual (map‚ÇÇ f h‚ÇÄ h‚ÇÅ s t) =
      map‚ÇÇ (fun a b => toDual <| f (ofDual a) <| ofDual b) (fun _ => (h‚ÇÄ _).dual)
        (fun _ => (h‚ÇÅ _).dual) (dual s) (dual t) :=
  rfl
#align nonempty_interval.dual_map‚ÇÇ NonemptyInterval.dual_map‚ÇÇ

variable [BoundedOrder Œ±]

instance : OrderTop (NonemptyInterval Œ±) where
  top := ‚ü®‚ü®‚ä•, ‚ä§‚ü©, bot_le‚ü©
  le_top _ := ‚ü®bot_le, le_top‚ü©

@[simp]
theorem dual_top : dual (‚ä§ : NonemptyInterval Œ±) = ‚ä§ :=
  rfl
#align nonempty_interval.dual_top NonemptyInterval.dual_top

end Preorder

section PartialOrder

variable [PartialOrder Œ±] [PartialOrder Œ≤] {s t : NonemptyInterval Œ±} {x : Œ± √ó Œ±} {a b : Œ±}

instance : PartialOrder (NonemptyInterval Œ±) :=
  PartialOrder.lift _ toDualProd_injective

/-- Consider a nonempty interval `[a, b]` as the set `[a, b]`. -/
def coeHom : NonemptyInterval Œ± ‚Ü™o Set Œ± :=
  OrderEmbedding.ofMapLEIff (fun s => Icc s.fst s.snd) fun s _ => Icc_subset_Icc_iff s.fst_le_snd
#align nonempty_interval.coe_hom NonemptyInterval.coeHom

instance setLike : SetLike (NonemptyInterval Œ±) Œ± where
  coe s := Icc s.fst s.snd
  coe_injective' := coeHom.injective

@[norm_cast] -- @[simp, norm_cast] -- Porting note: not in simpNF
theorem coe_subset_coe : (s : Set Œ±) ‚äÜ t ‚Üî (s : NonemptyInterval Œ±) ‚â§ t :=
  (@coeHom Œ± _).le_iff_le
#align nonempty_interval.coe_subset_coe NonemptyInterval.coe_subset_coe

@[norm_cast] -- @[simp, norm_cast] -- Porting note: not in simpNF
theorem coe_ssubset_coe : (s : Set Œ±) ‚äÇ t ‚Üî s < t :=
  (@coeHom Œ± _).lt_iff_lt
#align nonempty_interval.coe_ssubset_coe NonemptyInterval.coe_ssubset_coe

@[simp]
theorem coe_coeHom : (coeHom : NonemptyInterval Œ± ‚Üí Set Œ±) = ((‚Üë) : NonemptyInterval Œ± ‚Üí Set Œ±) :=
  rfl
#align nonempty_interval.coe_coe_hom NonemptyInterval.coe_coeHom

@[simp, norm_cast]
theorem coe_pure (a : Œ±) : (pure a : Set Œ±) = {a} :=
  Icc_self _
#align nonempty_interval.coe_pure NonemptyInterval.coe_pure

@[simp]
theorem mem_pure : b ‚àà pure a ‚Üî b = a := by
  rw [‚Üê SetLike.mem_coe, coe_pure, mem_singleton_iff]
  -- üéâ no goals
#align nonempty_interval.mem_pure NonemptyInterval.mem_pure

@[simp, norm_cast]
theorem coe_top [BoundedOrder Œ±] : ((‚ä§ : NonemptyInterval Œ±) : Set Œ±) = univ :=
  Icc_bot_top
#align nonempty_interval.coe_top NonemptyInterval.coe_top

@[simp, norm_cast]
theorem coe_dual (s : NonemptyInterval Œ±) : (dual s : Set Œ±·µí·µà) = ofDual ‚Åª¬π' s :=
  dual_Icc
#align nonempty_interval.coe_dual NonemptyInterval.coe_dual

theorem subset_coe_map (f : Œ± ‚Üío Œ≤) (s : NonemptyInterval Œ±) : f '' s ‚äÜ s.map f :=
  image_subset_iff.2 fun _ ha => ‚ü®f.mono ha.1, f.mono ha.2‚ü©
#align nonempty_interval.subset_coe_map NonemptyInterval.subset_coe_map

end PartialOrder

section Lattice

variable [Lattice Œ±]

instance : Sup (NonemptyInterval Œ±) :=
  ‚ü®fun s t => ‚ü®‚ü®s.fst ‚äì t.fst, s.snd ‚äî t.snd‚ü©, inf_le_left.trans <| s.fst_le_snd.trans le_sup_left‚ü©‚ü©

instance : SemilatticeSup (NonemptyInterval Œ±) :=
  toDualProd_injective.semilatticeSup _ fun _ _ => rfl

@[simp]
theorem fst_sup (s t : NonemptyInterval Œ±) : (s ‚äî t).fst = s.fst ‚äì t.fst :=
  rfl
#align nonempty_interval.fst_sup NonemptyInterval.fst_sup

@[simp]
theorem snd_sup (s t : NonemptyInterval Œ±) : (s ‚äî t).snd = s.snd ‚äî t.snd :=
  rfl
#align nonempty_interval.snd_sup NonemptyInterval.snd_sup

end Lattice

end NonemptyInterval

/-- The closed intervals in an order.

We represent intervals either as `‚ä•` or a nonempty interval given by its endpoints `fst`, `snd`.
To convert intervals to the set of elements between these endpoints, use the coercion
`Interval Œ± ‚Üí Set Œ±`. -/
@[reducible] -- Porting note: added reducible, it seems to help with coercions
def Interval (Œ± : Type*) [LE Œ±] :=
  WithBot (NonemptyInterval Œ±) -- deriving Inhabited, LE, OrderBot
#align interval Interval

namespace Interval

section LE

variable [LE Œ±] {s t : Interval Œ±}

-- porting note: previously found using `deriving`
instance : Inhabited (Interval Œ±) := WithBot.inhabited
instance : LE (Interval Œ±) := WithBot.le
instance : OrderBot (Interval Œ±) := WithBot.orderBot

instance : Coe (NonemptyInterval Œ±) (Interval Œ±) :=
  WithBot.coe

instance canLift : CanLift (Interval Œ±) (NonemptyInterval Œ±) (‚Üë) fun r => r ‚â† ‚ä• :=
  WithBot.canLift
#align interval.can_lift Interval.canLift

theorem coe_injective : Injective ((‚Üë) : NonemptyInterval Œ± ‚Üí Interval Œ±) :=
  WithBot.coe_injective
#align interval.coe_injective Interval.coe_injective

@[norm_cast] -- @[simp, norm_cast] -- Porting note: not in simpNF
theorem coe_inj {s t : NonemptyInterval Œ±} : (s : Interval Œ±) = t ‚Üî s = t :=
  WithBot.coe_inj
#align interval.coe_inj Interval.coe_inj

protected
theorem ¬´forall¬ª {p : Interval Œ± ‚Üí Prop} : (‚àÄ s, p s) ‚Üî p ‚ä• ‚àß ‚àÄ s : NonemptyInterval Œ±, p s :=
  Option.forall
#align interval.forall Interval.forall

protected
theorem ¬´exists¬ª {p : Interval Œ± ‚Üí Prop} : (‚àÉ s, p s) ‚Üî p ‚ä• ‚à® ‚àÉ s : NonemptyInterval Œ±, p s :=
  Option.exists
#align interval.exists Interval.exists

instance [IsEmpty Œ±] : Unique (Interval Œ±) :=
  inferInstanceAs <| Unique (Option _)

/-- Turn an interval into an interval in the dual order. -/
def dual : Interval Œ± ‚âÉ Interval Œ±·µí·µà :=
  NonemptyInterval.dual.optionCongr
#align interval.dual Interval.dual

end LE

section Preorder

variable [Preorder Œ±] [Preorder Œ≤] [Preorder Œ≥]

instance : Preorder (Interval Œ±) :=
  WithBot.preorder

/-- `{a}` as an interval. -/
def pure (a : Œ±) : Interval Œ± :=
  NonemptyInterval.pure a
#align interval.pure Interval.pure

theorem pure_injective : Injective (pure : Œ± ‚Üí Interval Œ±) :=
  coe_injective.comp NonemptyInterval.pure_injective
#align interval.pure_injective Interval.pure_injective

@[simp]
theorem dual_pure (a : Œ±) : dual (pure a) = pure (toDual a) :=
  rfl
#align interval.dual_pure Interval.dual_pure

@[simp]
theorem dual_bot : dual (‚ä• : Interval Œ±) = ‚ä• :=
  rfl
#align interval.dual_bot Interval.dual_bot

@[simp]
theorem pure_ne_bot {a : Œ±} : pure a ‚â† ‚ä• :=
  WithBot.coe_ne_bot
#align interval.pure_ne_bot Interval.pure_ne_bot

@[simp]
theorem bot_ne_pure {a : Œ±} : ‚ä• ‚â† pure a :=
  WithBot.bot_ne_coe
#align interval.bot_ne_pure Interval.bot_ne_pure

instance [Nonempty Œ±] : Nontrivial (Interval Œ±) :=
  Option.nontrivial

/-- Pushforward of intervals. -/
def map (f : Œ± ‚Üío Œ≤) : Interval Œ± ‚Üí Interval Œ≤ :=
  WithBot.map (NonemptyInterval.map f)
#align interval.map Interval.map

@[simp]
theorem map_pure (f : Œ± ‚Üío Œ≤) (a : Œ±) : (pure a).map f = pure (f a) :=
  rfl
#align interval.map_pure Interval.map_pure

@[simp]
theorem map_map (g : Œ≤ ‚Üío Œ≥) (f : Œ± ‚Üío Œ≤) (s : Interval Œ±) : (s.map f).map g = s.map (g.comp f) :=
  Option.map_map _ _ _
#align interval.map_map Interval.map_map

@[simp]
theorem dual_map (f : Œ± ‚Üío Œ≤) (s : Interval Œ±) : dual (s.map f) = s.dual.map (OrderHom.dual f) := by
  cases s
  -- ‚ä¢ ‚Üëdual (map f none) = map (‚ÜëOrderHom.dual f) (‚Üëdual none)
  ¬∑ rfl
    -- üéâ no goals
  ¬∑ exact WithBot.map_comm rfl _
    -- üéâ no goals
#align interval.dual_map Interval.dual_map

variable [BoundedOrder Œ±]

instance boundedOrder : BoundedOrder (Interval Œ±) :=
  WithBot.instBoundedOrder

@[simp]
theorem dual_top : dual (‚ä§ : Interval Œ±) = ‚ä§ :=
  rfl
#align interval.dual_top Interval.dual_top

end Preorder

section PartialOrder

variable [PartialOrder Œ±] [PartialOrder Œ≤] {s t : Interval Œ±} {a b : Œ±}

instance partialOrder : PartialOrder (Interval Œ±) :=
  WithBot.partialOrder

/-- Consider an interval `[a, b]` as the set `[a, b]`. -/
def coeHom : Interval Œ± ‚Ü™o Set Œ± :=
  OrderEmbedding.ofMapLEIff
    (fun s =>
      match s with
      | ‚ä• => ‚àÖ
      | some s => s)
    fun s t =>
    match s, t with
    | ‚ä•, _ => iff_of_true bot_le bot_le
    | some s, ‚ä• =>
      iff_of_false (fun h => s.coe_nonempty.ne_empty <| le_bot_iff.1 h) (WithBot.not_coe_le_bot _)
    | some _, some _ => (@NonemptyInterval.coeHom Œ± _).le_iff_le.trans WithBot.some_le_some.symm
#align interval.coe_hom Interval.coeHom

instance setLike : SetLike (Interval Œ±) Œ± where
  coe := coeHom
  coe_injective' := coeHom.injective

@[norm_cast] -- @[simp, norm_cast] -- Porting note: not in simpNF
theorem coe_subset_coe : (s : Set Œ±) ‚äÜ t ‚Üî s ‚â§ t :=
  (@coeHom Œ± _).le_iff_le
#align interval.coe_subset_coe Interval.coe_subset_coe

@[norm_cast] -- @[simp, norm_cast] -- Porting note: not in simpNF
theorem coe_sSubset_coe : (s : Set Œ±) ‚äÇ t ‚Üî s < t :=
  (@coeHom Œ± _).lt_iff_lt
#align interval.coe_ssubset_coe Interval.coe_sSubset_coe

@[simp, norm_cast]
theorem coe_pure (a : Œ±) : (pure a : Set Œ±) = {a} :=
  Icc_self _
#align interval.coe_pure Interval.coe_pure

@[simp, norm_cast]
theorem coe_coe (s : NonemptyInterval Œ±) : ((s : Interval Œ±) : Set Œ±) = s :=
  rfl
#align interval.coe_coe Interval.coe_coe

@[simp, norm_cast]
theorem coe_bot : ((‚ä• : Interval Œ±) : Set Œ±) = ‚àÖ :=
  rfl
#align interval.coe_bot Interval.coe_bot

@[simp, norm_cast]
theorem coe_top [BoundedOrder Œ±] : ((‚ä§ : Interval Œ±) : Set Œ±) = univ :=
  Icc_bot_top
#align interval.coe_top Interval.coe_top

@[simp, norm_cast]
theorem coe_dual (s : Interval Œ±) : (dual s : Set Œ±·µí·µà) = ofDual ‚Åª¬π' s := by
  cases s with
  | none => rfl
  | some s‚ÇÄ => exact NonemptyInterval.coe_dual s‚ÇÄ
#align interval.coe_dual Interval.coe_dual

theorem subset_coe_map (f : Œ± ‚Üío Œ≤) : ‚àÄ s : Interval Œ±, f '' s ‚äÜ s.map f
  | ‚ä• => by simp
            -- üéâ no goals
  | (s : NonemptyInterval Œ±) => s.subset_coe_map _
#align interval.subset_coe_map Interval.subset_coe_map

@[simp]
theorem mem_pure : b ‚àà pure a ‚Üî b = a := by rw [‚Üê SetLike.mem_coe, coe_pure, mem_singleton_iff]
                                            -- üéâ no goals
#align interval.mem_pure Interval.mem_pure

theorem mem_pure_self (a : Œ±) : a ‚àà pure a :=
  mem_pure.2 rfl
#align interval.mem_pure_self Interval.mem_pure_self

end PartialOrder

section Lattice

variable [Lattice Œ±]

instance semilatticeSup : SemilatticeSup (Interval Œ±) :=
  WithBot.semilatticeSup

section Decidable

variable [@DecidableRel Œ± (¬∑ ‚â§ ¬∑)]

instance lattice : Lattice (Interval Œ±) :=
  { Interval.semilatticeSup with
    inf := fun s t =>
      match s, t with
      | ‚ä•, _ => ‚ä•
      | _, ‚ä• => ‚ä•
      | some s, some t =>
        if h : s.fst ‚â§ t.snd ‚àß t.fst ‚â§ s.snd then
          some
            ‚ü®‚ü®s.fst ‚äî t.fst, s.snd ‚äì t.snd‚ü©,
              sup_le (le_inf s.fst_le_snd h.1) <| le_inf h.2 t.fst_le_snd‚ü©
        else ‚ä•
    inf_le_left := fun s t =>
      match s, t with
      | ‚ä•, ‚ä• => bot_le
      | ‚ä•, some _ => bot_le
      | some s, ‚ä• => bot_le
      | some s, some t => by
        change dite _ _ _ ‚â§ _
        -- ‚ä¢ (if h : s.fst ‚â§ t.snd ‚àß t.fst ‚â§ s.snd then some { toProd := (s.fst ‚äî t.fst,  ‚Ä¶
        split_ifs
        -- ‚ä¢ some { toProd := (s.fst ‚äî t.fst, s.snd ‚äì t.snd), fst_le_snd := (_ : s.fst ‚äî  ‚Ä¶
        ¬∑ exact WithBot.some_le_some.2 ‚ü®le_sup_left, inf_le_left‚ü©
          -- üéâ no goals
        ¬∑ exact bot_le
          -- üéâ no goals
    inf_le_right := fun s t =>
      match s, t with
      | ‚ä•, ‚ä• => bot_le
      | ‚ä•, some t => bot_le
      | some _, ‚ä• => bot_le
      | some s, some t => by
        change dite _ _ _ ‚â§ _
        -- ‚ä¢ (if h : s.fst ‚â§ t.snd ‚àß t.fst ‚â§ s.snd then some { toProd := (s.fst ‚äî t.fst,  ‚Ä¶
        split_ifs
        -- ‚ä¢ some { toProd := (s.fst ‚äî t.fst, s.snd ‚äì t.snd), fst_le_snd := (_ : s.fst ‚äî  ‚Ä¶
        ¬∑ exact WithBot.some_le_some.2 ‚ü®le_sup_right, inf_le_right‚ü©
          -- üéâ no goals
        ¬∑ exact bot_le
          -- üéâ no goals
    le_inf := fun s t c =>
      match s, t, c with
      | ‚ä•, t, c => fun _ _ => bot_le
      | some s, t, c => fun hb hc => by
        lift t to NonemptyInterval Œ± using ne_bot_of_le_ne_bot WithBot.coe_ne_bot hb
        -- ‚ä¢ some s ‚â§ ‚Üët ‚äì c
        lift c to NonemptyInterval Œ± using ne_bot_of_le_ne_bot WithBot.coe_ne_bot hc
        -- ‚ä¢ some s ‚â§ ‚Üët ‚äì ‚Üëc
        change _ ‚â§ dite _ _ _
        -- ‚ä¢ some s ‚â§ if h : t.fst ‚â§ c.snd ‚àß c.fst ‚â§ t.snd then some { toProd := (t.fst ‚äî ‚Ä¶
        -- Porting note: was `simp only` but that fails to use the second lemma.
        rw [WithBot.some_eq_coe, WithBot.coe_le_coe] at hb hc
        -- ‚ä¢ some s ‚â§ if h : t.fst ‚â§ c.snd ‚àß c.fst ‚â§ t.snd then some { toProd := (t.fst ‚äî ‚Ä¶
        simp only [WithBot.some_eq_coe, WithBot.coe_le_coe] -- at hb hc ‚ä¢
        -- ‚ä¢ ‚Üës ‚â§ if h : t.fst ‚â§ c.snd ‚àß c.fst ‚â§ t.snd then ‚Üë{ toProd := (t.fst ‚äî c.fst,  ‚Ä¶
        rw [dif_pos, WithBot.coe_le_coe]
        exact ‚ü®sup_le hb.1 hc.1, le_inf hb.2 hc.2‚ü©
        -- ‚ä¢ t.fst ‚â§ c.snd ‚àß c.fst ‚â§ t.snd
        -- Porting note: had to add the next 6 lines including the changes because
        -- it seems that lean cannot automatically turn `NonemptyInterval.toDualProd s`
        -- into `s.toProd` anymore.
        rcases hb with ‚ü®hb‚ÇÅ, hb‚ÇÇ‚ü©
        -- ‚ä¢ t.fst ‚â§ c.snd ‚àß c.fst ‚â§ t.snd
        rcases hc with ‚ü®hc‚ÇÅ, hc‚ÇÇ‚ü©
        -- ‚ä¢ t.fst ‚â§ c.snd ‚àß c.fst ‚â§ t.snd
        change t.toProd.fst ‚â§ s.toProd.fst at hb‚ÇÅ
        -- ‚ä¢ t.fst ‚â§ c.snd ‚àß c.fst ‚â§ t.snd
        change s.toProd.snd ‚â§ t.toProd.snd at hb‚ÇÇ
        -- ‚ä¢ t.fst ‚â§ c.snd ‚àß c.fst ‚â§ t.snd
        change c.toProd.fst ‚â§ s.toProd.fst at hc‚ÇÅ
        -- ‚ä¢ t.fst ‚â§ c.snd ‚àß c.fst ‚â§ t.snd
        change s.toProd.snd ‚â§ c.toProd.snd at hc‚ÇÇ
        -- ‚ä¢ t.fst ‚â§ c.snd ‚àß c.fst ‚â§ t.snd
        -- Porting note: originally it just had `hb.1` etc. in this next line
        exact ‚ü®hb‚ÇÅ.trans <| s.fst_le_snd.trans hc‚ÇÇ, hc‚ÇÅ.trans <| s.fst_le_snd.trans hb‚ÇÇ‚ü© }
        -- üéâ no goals

@[simp, norm_cast]
theorem coe_inf (s t : Interval Œ±) : (‚Üë(s ‚äì t) : Set Œ±) = ‚Üës ‚à© ‚Üët := by
  cases s with
  | none =>
    rw [WithBot.none_eq_bot, bot_inf_eq]
    exact (empty_inter _).symm
  | some s =>
    cases t with
    | none =>
      rw [WithBot.none_eq_bot, inf_bot_eq]
      exact (inter_empty _).symm
    | some t =>
      refine' (_ : setLike.coe (dite
        -- Porting note: Needed to fill this first `_` explicitly.
        (s.toProd.fst ‚â§ t.toProd.snd ‚àß t.toProd.fst ‚â§ s.toProd.snd)
        _ _) = _).trans Icc_inter_Icc.symm
      split_ifs with h
      ¬∑ rfl
      ¬∑ exact (Icc_eq_empty fun H =>
          h ‚ü®le_sup_left.trans <| H.trans inf_le_right,
          le_sup_right.trans <| H.trans inf_le_left‚ü©).symm
#align interval.coe_inf Interval.coe_inf

end Decidable

@[simp, norm_cast]
theorem disjoint_coe (s t : Interval Œ±) : Disjoint (s : Set Œ±) t ‚Üî Disjoint s t := by
  classical
    rw [disjoint_iff_inf_le, disjoint_iff_inf_le, ‚Üê coe_subset_coe, coe_inf]
    rfl
#align interval.disjoint_coe Interval.disjoint_coe

end Lattice

end Interval

namespace NonemptyInterval

section Preorder

variable [Preorder Œ±] {s : NonemptyInterval Œ±} {a : Œ±}

@[simp, norm_cast]
theorem coe_pure_interval (a : Œ±) : (pure a : Interval Œ±) = Interval.pure a :=
  rfl
#align nonempty_interval.coe_pure_interval NonemptyInterval.coe_pure_interval

@[simp, norm_cast]
theorem coe_eq_pure : (s : Interval Œ±) = Interval.pure a ‚Üî s = pure a := by
  rw [‚Üê Interval.coe_inj, coe_pure_interval]
  -- üéâ no goals
#align nonempty_interval.coe_eq_pure NonemptyInterval.coe_eq_pure

@[simp, norm_cast]
theorem coe_top_interval [BoundedOrder Œ±] : ((‚ä§ : NonemptyInterval Œ±) : Interval Œ±) = ‚ä§ :=
  rfl
#align nonempty_interval.coe_top_interval NonemptyInterval.coe_top_interval

end Preorder

@[simp, norm_cast]
theorem mem_coe_interval [PartialOrder Œ±] {s : NonemptyInterval Œ±} {x : Œ±} :
    x ‚àà (s : Interval Œ±) ‚Üî x ‚àà s :=
  Iff.rfl
#align nonempty_interval.mem_coe_interval NonemptyInterval.mem_coe_interval

@[simp, norm_cast]
theorem coe_sup_interval [Lattice Œ±] (s t : NonemptyInterval Œ±) :
    (‚Üë(s ‚äî t) : Interval Œ±) = ‚Üës ‚äî ‚Üët :=
  rfl
#align nonempty_interval.coe_sup_interval NonemptyInterval.coe_sup_interval

end NonemptyInterval

namespace Interval

section CompleteLattice

variable [CompleteLattice Œ±]

noncomputable instance completeLattice [@DecidableRel Œ± (¬∑ ‚â§ ¬∑)] :
    CompleteLattice (Interval Œ±) := by
  classical
  exact
      { Interval.lattice, Interval.boundedOrder with
        sSup := fun S =>
          if h : S ‚äÜ {‚ä•} then ‚ä•
          else
            some
              ‚ü®‚ü®‚®Ö (s : NonemptyInterval Œ±) (_ : ‚Üës ‚àà S), s.fst,
                  ‚®Ü (s : NonemptyInterval Œ±) (_ : ‚Üës ‚àà S), s.snd‚ü©, by
                obtain ‚ü®s, hs, ha‚ü© := not_subset.1 h
                lift s to NonemptyInterval Œ± using ha
                exact iInf‚ÇÇ_le_of_le s hs (le_iSup‚ÇÇ_of_le s hs s.fst_le_snd)‚ü©
        le_sSup := fun s s ha => by
          dsimp only -- Porting note: added
          split_ifs with h
          ¬∑ exact (h ha).le
          cases s
          ¬∑ exact bot_le
          ¬∑ -- Porting note: This case was
            -- `exact WithBot.some_le_some.2 ‚ü®iInf‚ÇÇ_le _ ha, le_iSup‚ÇÇ_of_le _ ha le_rfl‚ü©`
            -- but there seems to be a defEq-problem at `iInf‚ÇÇ_le` that lean cannot resolve yet.
            apply WithBot.some_le_some.2
            constructor
            ¬∑ apply iInf‚ÇÇ_le
              exact ha
            ¬∑ exact le_iSup‚ÇÇ_of_le _ ha le_rfl
        sSup_le := fun s s ha => by
          dsimp only -- Porting note: added
          split_ifs with h
          ¬∑ exact bot_le
          obtain ‚ü®b, hs, hb‚ü© := not_subset.1 h
          lift s to NonemptyInterval Œ± using ne_bot_of_le_ne_bot hb (ha _ hs)
          exact
            WithBot.coe_le_coe.2
              ‚ü®le_iInf‚ÇÇ fun c hc => (WithBot.coe_le_coe.1 <| ha _ hc).1,
                iSup‚ÇÇ_le fun c hc => (WithBot.coe_le_coe.1 <| ha _ hc).2‚ü©
        sInf := fun S =>
          if h :
              ‚ä• ‚àâ S ‚àß
                ‚àÄ ‚¶És : NonemptyInterval Œ±‚¶Ñ,
                  ‚Üës ‚àà S ‚Üí ‚àÄ ‚¶Ét : NonemptyInterval Œ±‚¶Ñ, ‚Üët ‚àà S ‚Üí s.fst ‚â§ t.snd then
            some
              ‚ü®‚ü®‚®Ü (s : NonemptyInterval Œ±) (_ : ‚Üës ‚àà S), s.fst,
                  ‚®Ö (s : NonemptyInterval Œ±) (_ : ‚Üës ‚àà S), s.snd‚ü©,
                iSup‚ÇÇ_le fun s hs => le_iInf‚ÇÇ <| h.2 hs‚ü©
          else ‚ä•
        sInf_le := fun s‚ÇÅ s ha => by
          dsimp only -- Porting note: added
          split_ifs with h
          ¬∑ lift s to NonemptyInterval Œ± using ne_of_mem_of_not_mem ha h.1
            -- Porting note: Lean failed to figure out the function `f` by itself,
            -- so I added it through manually
            let f := fun (s : NonemptyInterval Œ±) (_ : ‚Üës ‚àà s‚ÇÅ) => s.toProd.fst
            exact WithBot.coe_le_coe.2 ‚ü®le_iSup‚ÇÇ (f := f) s ha, iInf‚ÇÇ_le s ha‚ü©
          ¬∑ exact bot_le
        le_sInf := by
          intro S s ha
          cases s with
          | none => exact bot_le
          | some s =>
            dsimp -- Porting note: added
            split_ifs with h
            ¬∑ exact WithBot.some_le_some.2
                ‚ü®iSup‚ÇÇ_le fun t hb => (WithBot.coe_le_coe.1 <| ha _ hb).1,
                  le_iInf‚ÇÇ fun t hb => (WithBot.coe_le_coe.1 <| ha _ hb).2‚ü©
            ¬∑ rw [not_and_or, not_not] at h
              rcases h with h | h
              ¬∑ exact ha _ h
              ¬∑ -- Porting note: ungolfed, due to identification problems
                -- between `toProd` and `toDualProd`. Original mathport output:
                -- cases h fun t hb c hc =>
                --   (WithBot.coe_le_coe.1 <| ha _ hb).1.trans <|
                --     s.fst_le_snd.trans (WithBot.coe_le_coe.1 <| ha _ hc).2 }
                exfalso
                apply h
                intro b hb c hc
                have h‚ÇÅ := (WithBot.coe_le_coe.1 <| ha _ hb).1
                repeat rw [NonemptyInterval.toDualProd_apply] at h‚ÇÅ
                rw [OrderDual.toDual_le_toDual] at h‚ÇÅ
                exact h‚ÇÅ.trans (s.fst_le_snd.trans (WithBot.coe_le_coe.1 <| ha _ hc).2)
  }

@[simp, norm_cast]
theorem coe_sInf [@DecidableRel Œ± (¬∑ ‚â§ ¬∑)] (S : Set (Interval Œ±)) :
    ‚Üë(sInf S) = ‚ãÇ s ‚àà S, (s : Set Œ±) := by
  classical -- Porting note: added
  -- Porting note: this `change` was
  -- change ‚Üë (dite _ _ _) = _
  change ((dite _ _ _ : Interval Œ±) : Set Œ±) = ‚ãÇ (s : Interval Œ±) (_ : s ‚àà S), (s : Set Œ±)
  split_ifs with h
  ¬∑ ext
    simp [WithBot.some_eq_coe, Interval.forall, h.1, ‚Üê forall_and, ‚Üê NonemptyInterval.mem_def]
  simp_rw [not_and_or, Classical.not_not] at h
  rcases h with h | h
  ¬∑ refine' (eq_empty_of_subset_empty _).symm
    exact iInter‚ÇÇ_subset_of_subset _ h Subset.rfl
  ¬∑ refine' (not_nonempty_iff_eq_empty.1 _).symm
    rintro ‚ü®x, hx‚ü©
    rw [mem_iInter‚ÇÇ] at hx
    exact h fun s ha t hb => (hx _ ha).1.trans (hx _ hb).2
#align interval.coe_Inf Interval.coe_sInf

@[simp, norm_cast]
theorem coe_iInf [@DecidableRel Œ± (¬∑ ‚â§ ¬∑)] (f : Œπ ‚Üí Interval Œ±) :
    ‚Üë(‚®Ö i, f i) = ‚ãÇ i, (f i : Set Œ±) := by simp [iInf]
                                           -- üéâ no goals
#align interval.coe_infi Interval.coe_iInf

-- @[simp] -- Porting note: not in simpNF
@[norm_cast]
theorem coe_iInf‚ÇÇ [@DecidableRel Œ± (¬∑ ‚â§ ¬∑)] (f : ‚àÄ i, Œ∫ i ‚Üí Interval Œ±) :
    ‚Üë(‚®Ö (i) (j), f i j) = ‚ãÇ (i) (j), (f i j : Set Œ±) := by simp_rw [coe_iInf]
                                                           -- üéâ no goals
#align interval.coe_infi‚ÇÇ Interval.coe_iInf‚ÇÇ

end CompleteLattice

end Interval
