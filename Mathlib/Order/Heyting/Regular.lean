/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Order.GaloisConnection

#align_import order.heyting.regular from "leanprover-community/mathlib"@"09597669f02422ed388036273d8848119699c22f"

/-!
# Heyting regular elements

This file defines Heyting regular elements, elements of a Heyting algebra that are their own double
complement, and proves that they form a boolean algebra.

From a logic standpoint, this means that we can perform classical logic within intuitionistic logic
by simply double-negating all propositions. This is practical for synthetic computability theory.

## Main declarations

* `IsRegular`: `a` is Heyting-regular if `aá¶œá¶œ = a`.
* `Regular`: The subtype of Heyting-regular elements.
* `Regular.BooleanAlgebra`: Heyting-regular elements form a boolean algebra.

## References

* [Francis Borceux, *Handbook of Categorical Algebra III*][borceux-vol3]
-/


open Function

variable {Î± : Type*}

namespace Heyting

section HasCompl

variable [HasCompl Î±] {a : Î±}

/-- An element of a Heyting algebra is regular if its double complement is itself. -/
def IsRegular (a : Î±) : Prop :=
  aá¶œá¶œ = a
#align heyting.is_regular Heyting.IsRegular

protected theorem IsRegular.eq : IsRegular a â†’ aá¶œá¶œ = a :=
  id
#align heyting.is_regular.eq Heyting.IsRegular.eq

instance IsRegular.decidablePred [DecidableEq Î±] : @DecidablePred Î± IsRegular := fun _ =>
  â€¹DecidableEq Î±â€º _ _
#align heyting.is_regular.decidable_pred Heyting.IsRegular.decidablePred

end HasCompl

section HeytingAlgebra

variable [HeytingAlgebra Î±] {a b : Î±}

theorem isRegular_bot : IsRegular (âŠ¥ : Î±) := by rw [IsRegular, compl_bot, compl_top]
                                                -- ðŸŽ‰ no goals
#align heyting.is_regular_bot Heyting.isRegular_bot

theorem isRegular_top : IsRegular (âŠ¤ : Î±) := by rw [IsRegular, compl_top, compl_bot]
                                                -- ðŸŽ‰ no goals
#align heyting.is_regular_top Heyting.isRegular_top

theorem IsRegular.inf (ha : IsRegular a) (hb : IsRegular b) : IsRegular (a âŠ“ b) := by
  rw [IsRegular, compl_compl_inf_distrib, ha.eq, hb.eq]
  -- ðŸŽ‰ no goals
#align heyting.is_regular.inf Heyting.IsRegular.inf

theorem IsRegular.himp (ha : IsRegular a) (hb : IsRegular b) : IsRegular (a â‡¨ b) := by
  rw [IsRegular, compl_compl_himp_distrib, ha.eq, hb.eq]
  -- ðŸŽ‰ no goals
#align heyting.is_regular.himp Heyting.IsRegular.himp

theorem isRegular_compl (a : Î±) : IsRegular aá¶œ :=
  compl_compl_compl _
#align heyting.is_regular_compl Heyting.isRegular_compl

protected theorem IsRegular.disjoint_compl_left_iff (ha : IsRegular a) : Disjoint aá¶œ b â†” b â‰¤ a :=
  by rw [â† le_compl_iff_disjoint_left, ha.eq]
     -- ðŸŽ‰ no goals
#align heyting.is_regular.disjoint_compl_left_iff Heyting.IsRegular.disjoint_compl_left_iff

protected theorem IsRegular.disjoint_compl_right_iff (hb : IsRegular b) : Disjoint a bá¶œ â†” a â‰¤ b :=
  by rw [â† le_compl_iff_disjoint_right, hb.eq]
     -- ðŸŽ‰ no goals
#align heyting.is_regular.disjoint_compl_right_iff Heyting.IsRegular.disjoint_compl_right_iff

-- See note [reducible non-instances]
/-- A Heyting algebra with regular excluded middle is a boolean algebra. -/
@[reducible]
def _root_.BooleanAlgebra.ofRegular (h : âˆ€ a : Î±, IsRegular (a âŠ” aá¶œ)) : BooleanAlgebra Î± :=
  have : âˆ€ a : Î±, IsCompl a aá¶œ := fun a =>
    âŸ¨disjoint_compl_right,
      codisjoint_iff.2 <| by erw [â† (h a), compl_sup, inf_compl_eq_bot, compl_bot]âŸ©
                             -- ðŸŽ‰ no goals
  { â€¹HeytingAlgebra Î±â€º,
    GeneralizedHeytingAlgebra.toDistribLattice with
    himp_eq := fun a b =>
      eq_of_forall_le_iff fun c => le_himp_iff.trans (this _).le_sup_right_iff_inf_left_le.symm
    inf_compl_le_bot := fun a => (this _).1.le_bot
    top_le_sup_compl := fun a => (this _).2.top_le }
#align boolean_algebra.of_regular BooleanAlgebra.ofRegular

variable (Î±)

/-- The boolean algebra of Heyting regular elements. -/
def Regular : Type _ :=
  { a : Î± // IsRegular a }
#align heyting.regular Heyting.Regular

variable {Î±}

namespace Regular

--Porting note: `val` and `prop` are new
/-- The coercion `Regular Î± â†’ Î±` -/
@[coe] def val : Regular Î± â†’ Î± :=
  Subtype.val

theorem prop : âˆ€ a : Regular Î±, IsRegular a.val := Subtype.prop

instance : Coe (Regular Î±) Î± := âŸ¨Regular.valâŸ©

theorem coe_injective : Injective ((â†‘) : Regular Î± â†’ Î±) :=
  Subtype.coe_injective
#align heyting.regular.coe_injective Heyting.Regular.coe_injective

@[simp]
theorem coe_inj {a b : Regular Î±} : (a : Î±) = b â†” a = b :=
  Subtype.coe_inj
#align heyting.regular.coe_inj Heyting.Regular.coe_inj

instance top : Top (Regular Î±) :=
  âŸ¨âŸ¨âŠ¤, isRegular_topâŸ©âŸ©

instance bot : Bot (Regular Î±) :=
  âŸ¨âŸ¨âŠ¥, isRegular_botâŸ©âŸ©

instance inf : Inf (Regular Î±) :=
  âŸ¨fun a b => âŸ¨a âŠ“ b, a.2.inf b.2âŸ©âŸ©

instance himp : HImp (Regular Î±) :=
  âŸ¨fun a b => âŸ¨a â‡¨ b, a.2.himp b.2âŸ©âŸ©

instance hasCompl : HasCompl (Regular Î±) :=
  âŸ¨fun a => âŸ¨aá¶œ, isRegular_compl _âŸ©âŸ©

@[simp, norm_cast]
theorem coe_top : ((âŠ¤ : Regular Î±) : Î±) = âŠ¤ :=
  rfl
#align heyting.regular.coe_top Heyting.Regular.coe_top

@[simp, norm_cast]
theorem coe_bot : ((âŠ¥ : Regular Î±) : Î±) = âŠ¥ :=
  rfl
#align heyting.regular.coe_bot Heyting.Regular.coe_bot

@[simp, norm_cast]
theorem coe_inf (a b : Regular Î±) : (â†‘(a âŠ“ b) : Î±) = (a : Î±) âŠ“ b :=
  rfl
#align heyting.regular.coe_inf Heyting.Regular.coe_inf

@[simp, norm_cast]
theorem coe_himp (a b : Regular Î±) : (â†‘(a â‡¨ b) : Î±) = (a : Î±) â‡¨ b :=
  rfl
#align heyting.regular.coe_himp Heyting.Regular.coe_himp

@[simp, norm_cast]
theorem coe_compl (a : Regular Î±) : (â†‘aá¶œ : Î±) = (a : Î±)á¶œ :=
  rfl
#align heyting.regular.coe_compl Heyting.Regular.coe_compl

instance : Inhabited (Regular Î±) :=
  âŸ¨âŠ¥âŸ©

instance : SemilatticeInf (Regular Î±) :=
  coe_injective.semilatticeInf _ coe_inf

instance boundedOrder : BoundedOrder (Regular Î±) :=
  BoundedOrder.lift ((â†‘) : Regular Î± â†’ Î±) (fun _ _ => id) coe_top coe_bot

@[simp, norm_cast]
theorem coe_le_coe {a b : Regular Î±} : (a : Î±) â‰¤ b â†” a â‰¤ b :=
  Iff.rfl
#align heyting.regular.coe_le_coe Heyting.Regular.coe_le_coe

@[simp, norm_cast]
theorem coe_lt_coe {a b : Regular Î±} : (a : Î±) < b â†” a < b :=
  Iff.rfl
#align heyting.regular.coe_lt_coe Heyting.Regular.coe_lt_coe

/-- **Regularization** of `a`. The smallest regular element greater than `a`. -/
def toRegular : Î± â†’o Regular Î± :=
  âŸ¨fun a => âŸ¨aá¶œá¶œ, isRegular_compl _âŸ©, fun _ _ h =>
    coe_le_coe.1 <| compl_le_compl <| compl_le_compl hâŸ©
#align heyting.regular.to_regular Heyting.Regular.toRegular

@[simp, norm_cast]
theorem coe_toRegular (a : Î±) : (toRegular a : Î±) = aá¶œá¶œ :=
  rfl
#align heyting.regular.coe_to_regular Heyting.Regular.coe_toRegular

@[simp]
theorem toRegular_coe (a : Regular Î±) : toRegular (a : Î±) = a :=
  coe_injective a.2
#align heyting.regular.to_regular_coe Heyting.Regular.toRegular_coe

/-- The Galois insertion between `Regular.toRegular` and `coe`. -/
def gi : GaloisInsertion toRegular ((â†‘) : Regular Î± â†’ Î±)
    where
  choice a ha := âŸ¨a, ha.antisymm le_compl_complâŸ©
  gc _ b :=
    coe_le_coe.symm.trans <|
      âŸ¨le_compl_compl.trans, fun h => (compl_anti <| compl_anti h).trans_eq b.2âŸ©
  le_l_u _ := le_compl_compl
  choice_eq _ ha := coe_injective <| le_compl_compl.antisymm ha
#align heyting.regular.gi Heyting.Regular.gi

instance lattice : Lattice (Regular Î±) :=
  gi.liftLattice

@[simp, norm_cast]
theorem coe_sup (a b : Regular Î±) : (â†‘(a âŠ” b) : Î±) = ((a : Î±) âŠ” b)á¶œá¶œ :=
  rfl
#align heyting.regular.coe_sup Heyting.Regular.coe_sup

instance : BooleanAlgebra (Regular Î±) :=
  { Regular.lattice, Regular.boundedOrder, Regular.himp,
    Regular.hasCompl with
    le_sup_inf := fun a b c =>
      coe_le_coe.1 <| by
        dsimp
        -- âŠ¢ (â†‘a âŠ” â†‘b)á¶œá¶œ âŠ“ (â†‘a âŠ” â†‘c)á¶œá¶œ â‰¤ (â†‘a âŠ” â†‘b âŠ“ â†‘c)á¶œá¶œ
        rw [sup_inf_left, compl_compl_inf_distrib]
        -- ðŸŽ‰ no goals
    inf_compl_le_bot := fun a => coe_le_coe.1 <| disjoint_iff_inf_le.1 disjoint_compl_right
    top_le_sup_compl := fun a =>
      coe_le_coe.1 <| by
        dsimp
        -- âŠ¢ âŠ¤ â‰¤ (â†‘a âŠ” (â†‘a)á¶œ)á¶œá¶œ
        rw [compl_sup, inf_compl_eq_bot, compl_bot]
        -- ðŸŽ‰ no goals
    himp_eq := fun a b =>
      coe_injective
        (by
          dsimp
          -- âŠ¢ â†‘a â‡¨ â†‘b = (â†‘b âŠ” (â†‘a)á¶œ)á¶œá¶œ
          rw [compl_sup, a.prop.eq]
          -- âŠ¢ â†‘a â‡¨ â†‘b = ((â†‘b)á¶œ âŠ“ â†‘a)á¶œ
          refine' eq_of_forall_le_iff fun c => le_himp_iff.trans _
          -- âŠ¢ c âŠ“ â†‘a â‰¤ â†‘b â†” c â‰¤ ((â†‘b)á¶œ âŠ“ â†‘a)á¶œ
          rw [le_compl_iff_disjoint_right, disjoint_left_comm]
          -- âŠ¢ c âŠ“ â†‘a â‰¤ â†‘b â†” Disjoint (â†‘b)á¶œ (c âŠ“ â†‘a)
          rw [b.prop.disjoint_compl_left_iff]) }
          -- ðŸŽ‰ no goals

@[simp, norm_cast]
theorem coe_sdiff (a b : Regular Î±) : (â†‘(a \ b) : Î±) = (a : Î±) âŠ“ bá¶œ :=
  rfl
#align heyting.regular.coe_sdiff Heyting.Regular.coe_sdiff

end Regular

end HeytingAlgebra

variable [BooleanAlgebra Î±]

theorem isRegular_of_boolean : âˆ€ a : Î±, IsRegular a :=
  compl_compl
#align heyting.is_regular_of_boolean Heyting.isRegular_of_boolean

/-- A decidable proposition is intuitionistically Heyting-regular. -/
--Porting note: removed @[nolint decidable_classical]
theorem isRegular_of_decidable (p : Prop) [Decidable p] : IsRegular p :=
  propext <| Decidable.not_not
#align heyting.is_regular_of_decidable Heyting.isRegular_of_decidable

end Heyting
