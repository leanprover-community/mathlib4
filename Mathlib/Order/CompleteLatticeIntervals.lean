/-
Copyright (c) 2022 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.Order.ConditionallyCompleteLattice.Basic
import Mathlib.Data.Set.Intervals.OrdConnected

#align_import order.complete_lattice_intervals from "leanprover-community/mathlib"@"207cfac9fcd06138865b5d04f7091e46d9320432"

/-! # Subtypes of conditionally complete linear orders

In this file we give conditions on a subset of a conditionally complete linear order, to ensure that
the subtype is itself conditionally complete.

We check that an `OrdConnected` set satisfies these conditions.

## TODO

Add appropriate instances for all `Set.Ixx`. This requires a refactor that will allow different
default values for `sSup` and `sInf`.
-/


open Classical

open Set

variable {Î± : Type*} (s : Set Î±)

section SupSet

variable [SupSet Î±]

/-- `SupSet` structure on a nonempty subset `s` of an object with `SupSet`. This definition is
non-canonical (it uses `default s`); it should be used only as here, as an auxiliary instance in the
construction of the `ConditionallyCompleteLinearOrder` structure. -/
noncomputable def subsetSupSet [Inhabited s] : SupSet s where
  sSup t :=
    if ht : sSup ((â†‘) '' t : Set Î±) âˆˆ s
    then âŸ¨sSup ((â†‘) '' t : Set Î±), htâŸ©
    else default
#align subset_has_Sup subsetSupSet

attribute [local instance] subsetSupSet

@[simp]
theorem subset_sSup_def [Inhabited s] :
    @sSup s _ = fun t =>
      if ht : sSup ((â†‘) '' t : Set Î±) âˆˆ s
      then âŸ¨sSup ((â†‘) '' t : Set Î±), htâŸ©
      else default :=
  rfl
#align subset_Sup_def subset_sSup_def

theorem subset_sSup_of_within [Inhabited s] {t : Set s} (h : sSup ((â†‘) '' t : Set Î±) âˆˆ s) :
    sSup ((â†‘) '' t : Set Î±) = (@sSup s _ t : Î±) := by simp [dif_pos h]
                                                      -- ðŸŽ‰ no goals
#align subset_Sup_of_within subset_sSup_of_within

end SupSet

section InfSet

variable [InfSet Î±]

/-- `InfSet` structure on a nonempty subset `s` of an object with `InfSet`. This definition is
non-canonical (it uses `default s`); it should be used only as here, as an auxiliary instance in the
construction of the `ConditionallyCompleteLinearOrder` structure. -/
noncomputable def subsetInfSet [Inhabited s] : InfSet s where
  sInf t :=
    if ht : sInf ((â†‘) '' t : Set Î±) âˆˆ s
    then âŸ¨sInf ((â†‘) '' t : Set Î±), htâŸ©
    else default
#align subset_has_Inf subsetInfSet

attribute [local instance] subsetInfSet

@[simp]
theorem subset_sInf_def [Inhabited s] :
    @sInf s _ = fun t =>
      if ht : sInf ((â†‘) '' t : Set Î±) âˆˆ s
      then âŸ¨sInf ((â†‘) '' t : Set Î±), htâŸ© else
      default :=
  rfl
#align subset_Inf_def subset_sInf_def

theorem subset_sInf_of_within [Inhabited s] {t : Set s} (h : sInf ((â†‘) '' t : Set Î±) âˆˆ s) :
    sInf ((â†‘) '' t : Set Î±) = (@sInf s _ t : Î±) := by simp [dif_pos h]
                                                      -- ðŸŽ‰ no goals
#align subset_Inf_of_within subset_sInf_of_within

end InfSet

variable [ConditionallyCompleteLinearOrder Î±]

attribute [local instance] subsetSupSet

attribute [local instance] subsetInfSet

lemma sSup_subtype_eq_sSup_univ_of_not_bddAbove {s : Set Î±} [Inhabited s]
    (t : Set s) (ht : Â¬BddAbove t) : sSup t = sSup univ := by
  have A : âˆ€ (u : Set s), Â¬BddAbove u â†’ BddAbove (Subtype.val '' u) â†’
      sSup ((â†‘) '' u : Set Î±) âˆ‰ s := by
    intro u hu Hu
    contrapose! hu
    refine âŸ¨âŸ¨_, huâŸ©, ?_âŸ©
    rintro âŸ¨x, xsâŸ© hx
    simp only [Subtype.mk_le_mk]
    apply le_csSup Hu
    exact âŸ¨âŸ¨x, xsâŸ©, hx, rflâŸ©
  by_cases Ht : BddAbove ((â†‘) '' t : Set Î±)
  -- âŠ¢ sSup t = sSup univ
  Â· have I1 : sSup ((â†‘) '' t : Set Î±) âˆ‰ s := A t ht Ht
    -- âŠ¢ sSup t = sSup univ
    have I2 : sSup ((â†‘) '' (univ : Set s) : Set Î±) âˆ‰ s := by
      apply A
      Â· contrapose! ht; exact ht.mono (subset_univ _)
      Â· refine âŸ¨sSup ((â†‘) '' t : Set Î±), ?_âŸ©
        rintro - âŸ¨âŸ¨x, hxâŸ©, -, rflâŸ©
        simp [BddAbove, not_nonempty_iff_eq_empty] at ht
        have : âŸ¨x, hxâŸ© âˆ‰ upperBounds t := by simp [ht]
        obtain âŸ¨âŸ¨y, ysâŸ©, yt, hyâŸ© : âˆƒ y, y âˆˆ t âˆ§ { val := x, property := hx } < y :=
          by simpa only [Subtype.mk_le_mk, not_forall, not_le, exists_prop, exists_and_right,
            mem_upperBounds]
        refine le_trans (le_of_lt hy) ?_
        exact le_csSup Ht âŸ¨âŸ¨y, ysâŸ©, yt, rflâŸ©
    simp only [sSup, I1, I2, dite_false]
    -- ðŸŽ‰ no goals
  Â· have I : Â¬BddAbove ((â†‘) '' (univ : Set s) : Set Î±) := by
      contrapose! Ht; exact Ht.mono (image_subset Subtype.val (subset_univ _))
    have X : sSup ((â†‘) '' t : Set Î±) = sSup (univ : Set Î±) :=
      ConditionallyCompleteLinearOrder.csSup_of_not_bddAbove _ Ht
    have Y : sSup ((â†‘) '' (univ : Set s) : Set Î±) = sSup (univ : Set Î±) :=
      ConditionallyCompleteLinearOrder.csSup_of_not_bddAbove _ I
    simp only [sSup, X, Y]
    -- ðŸŽ‰ no goals

/-- For a nonempty subset of a conditionally complete linear order to be a conditionally complete
linear order, it suffices that it contain the `sSup` of all its nonempty bounded-above subsets, and
the `sInf` of all its nonempty bounded-below subsets.
See note [reducible non-instances]. -/
@[reducible]
noncomputable def subsetConditionallyCompleteLinearOrder [Inhabited s]
    (h_Sup : âˆ€ {t : Set s} (_ : t.Nonempty) (_h_bdd : BddAbove t), sSup ((â†‘) '' t : Set Î±) âˆˆ s)
    (h_Inf : âˆ€ {t : Set s} (_ : t.Nonempty) (_h_bdd : BddBelow t), sInf ((â†‘) '' t : Set Î±) âˆˆ s) :
    ConditionallyCompleteLinearOrder s :=
  { -- The following would be a more natural way to finish, but gives a "deep recursion" error:
      -- simpa [subset_Sup_of_within (h_Sup t)] using
      --   (strict_mono_coe s).monotone.le_cSup_image hct h_bdd,
    subsetSupSet s, subsetInfSet s, DistribLattice.toLattice, (inferInstance : LinearOrder s) with
    le_csSup := by
      rintro t c h_bdd hct
      -- âŠ¢ c â‰¤ sSup t
      rw [â† Subtype.coe_le_coe, â† subset_sSup_of_within s (h_Sup âŸ¨c, hctâŸ© h_bdd)]
      -- âŠ¢ â†‘c â‰¤ sSup (Subtype.val '' t)
      exact (Subtype.mono_coe _).le_csSup_image hct h_bdd
      -- ðŸŽ‰ no goals
    csSup_le := by
      rintro t B ht hB
      -- âŠ¢ sSup t â‰¤ B
      rw [â† Subtype.coe_le_coe, â† subset_sSup_of_within s (h_Sup ht âŸ¨B, hBâŸ©)]
      -- âŠ¢ sSup (Subtype.val '' t) â‰¤ â†‘B
      exact (Subtype.mono_coe s).csSup_image_le ht hB
      -- ðŸŽ‰ no goals
    le_csInf := by
      intro t B ht hB
      -- âŠ¢ B â‰¤ sInf t
      rw [â† Subtype.coe_le_coe, â† subset_sInf_of_within s (h_Inf ht âŸ¨B, hBâŸ©)]
      -- âŠ¢ â†‘B â‰¤ sInf (Subtype.val '' t)
      exact (Subtype.mono_coe s).le_csInf_image ht hB
      -- âŠ¢ sInf t â‰¤ c
      -- ðŸŽ‰ no goals
      -- âŠ¢ sInf (Subtype.val '' t) â‰¤ â†‘c
    csInf_le := by
      -- ðŸŽ‰ no goals
      rintro t c h_bdd hct
      rw [â† Subtype.coe_le_coe, â† subset_sInf_of_within s (h_Inf âŸ¨c, hctâŸ© h_bdd)]
      exact (Subtype.mono_coe s).csInf_image_le hct h_bdd
    csSup_of_not_bddAbove := sSup_subtype_eq_sSup_univ_of_not_bddAbove
    csInf_of_not_bddBelow := @sSup_subtype_eq_sSup_univ_of_not_bddAbove Î±áµ’áµˆ _ _ _ }
#align subset_conditionally_complete_linear_order subsetConditionallyCompleteLinearOrder

section OrdConnected

/-- The `sSup` function on a nonempty `OrdConnected` set `s` in a conditionally complete linear
order takes values within `s`, for all nonempty bounded-above subsets of `s`. -/
theorem sSup_within_of_ordConnected {s : Set Î±} [hs : OrdConnected s] â¦ƒt : Set sâ¦„ (ht : t.Nonempty)
    (h_bdd : BddAbove t) : sSup ((â†‘) '' t : Set Î±) âˆˆ s := by
  obtain âŸ¨c, hctâŸ© : âˆƒ c, c âˆˆ t := ht
  -- âŠ¢ sSup (Subtype.val '' t) âˆˆ s
  obtain âŸ¨B, hBâŸ© : âˆƒ B, B âˆˆ upperBounds t := h_bdd
  -- âŠ¢ sSup (Subtype.val '' t) âˆˆ s
  refine' hs.out c.2 B.2 âŸ¨_, _âŸ©
  -- âŠ¢ â†‘c â‰¤ sSup (Subtype.val '' t)
  Â· exact (Subtype.mono_coe s).le_csSup_image hct âŸ¨B, hBâŸ©
    -- ðŸŽ‰ no goals
  Â· exact (Subtype.mono_coe s).csSup_image_le âŸ¨c, hctâŸ© hB
    -- ðŸŽ‰ no goals
#align Sup_within_of_ord_connected sSup_within_of_ordConnected

/-- The `sInf` function on a nonempty `OrdConnected` set `s` in a conditionally complete linear
order takes values within `s`, for all nonempty bounded-below subsets of `s`. -/
theorem sInf_within_of_ordConnected {s : Set Î±} [hs : OrdConnected s] â¦ƒt : Set sâ¦„ (ht : t.Nonempty)
    (h_bdd : BddBelow t) : sInf ((â†‘) '' t : Set Î±) âˆˆ s := by
  obtain âŸ¨c, hctâŸ© : âˆƒ c, c âˆˆ t := ht
  -- âŠ¢ sInf (Subtype.val '' t) âˆˆ s
  obtain âŸ¨B, hBâŸ© : âˆƒ B, B âˆˆ lowerBounds t := h_bdd
  -- âŠ¢ sInf (Subtype.val '' t) âˆˆ s
  refine' hs.out B.2 c.2 âŸ¨_, _âŸ©
  -- âŠ¢ â†‘B â‰¤ sInf (Subtype.val '' t)
  Â· exact (Subtype.mono_coe s).le_csInf_image âŸ¨c, hctâŸ© hB
    -- ðŸŽ‰ no goals
  Â· exact (Subtype.mono_coe s).csInf_image_le hct âŸ¨B, hBâŸ©
    -- ðŸŽ‰ no goals
#align Inf_within_of_ord_connected sInf_within_of_ordConnected

/-- A nonempty `OrdConnected` set in a conditionally complete linear order is naturally a
conditionally complete linear order. -/
noncomputable instance ordConnectedSubsetConditionallyCompleteLinearOrder [Inhabited s]
    [OrdConnected s] : ConditionallyCompleteLinearOrder s :=
  subsetConditionallyCompleteLinearOrder s
    (fun h => sSup_within_of_ordConnected h)
    (fun h => sInf_within_of_ordConnected h)
#align ord_connected_subset_conditionally_complete_linear_order ordConnectedSubsetConditionallyCompleteLinearOrder

end OrdConnected
