/-
Copyright (c) 2023 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Data.Finset.Lattice
import Mathlib.Data.Fintype.Card

#align_import order.irreducible from "leanprover-community/mathlib"@"bf2428c9486c407ca38b5b3fb10b87dad0bc99fa"

/-!
# Irreducible and prime elements in an order

This file defines irreducible and prime elements in an order and shows that in a well-founded
lattice every element decomposes as a supremum of irreducible elements.

An element is sup-irreducible (resp. inf-irreducible) if it isn't `âŠ¥` and can't be written as the
supremum of any strictly smaller elements. An element is sup-prime (resp. inf-prime) if it isn't `âŠ¥`
and is greater than the supremum of any two elements less than it.

Primality implies irreducibility in general. The converse only holds in distributive lattices.
Both hold for all (non-minimal) elements in a linear order.

## Main declarations

* `SupIrred a`: Sup-irreducibility, `a` isn't minimal and `a = b âŠ” c â†’ a = b âˆ¨ a = c`
* `InfIrred a`: Inf-irreducibility, `a` isn't maximal and `a = b âŠ“ c â†’ a = b âˆ¨ a = c`
* `SupPrime a`: Sup-primality, `a` isn't minimal and `a â‰¤ b âŠ” c â†’ a â‰¤ b âˆ¨ a â‰¤ c`
* `InfIrred a`: Inf-primality, `a` isn't maximal and `a â‰¥ b âŠ“ c â†’ a â‰¥ b âˆ¨ a â‰¥ c`
* `exists_supIrred_decomposition`/`exists_infIrred_decomposition`: Decomposition into irreducibles
  in a well-founded semilattice.
-/


open Finset OrderDual

variable {Î¹ Î± : Type*}

/-! ### Irreducible and prime elements -/


section SemilatticeSup

variable [SemilatticeSup Î±] {a b c : Î±}

/-- A sup-irreducible element is a non-bottom element which isn't the supremum of anything smaller.
-/
def SupIrred (a : Î±) : Prop :=
  Â¬IsMin a âˆ§ âˆ€ â¦ƒb câ¦„, b âŠ” c = a â†’ b = a âˆ¨ c = a
#align sup_irred SupIrred

/-- A sup-prime element is a non-bottom element which isn't less than the supremum of anything
smaller. -/
def SupPrime (a : Î±) : Prop :=
  Â¬IsMin a âˆ§ âˆ€ â¦ƒb câ¦„, a â‰¤ b âŠ” c â†’ a â‰¤ b âˆ¨ a â‰¤ c
#align sup_prime SupPrime

theorem SupIrred.not_isMin (ha : SupIrred a) : Â¬IsMin a :=
  ha.1
#align sup_irred.not_is_min SupIrred.not_isMin

theorem SupPrime.not_isMin (ha : SupPrime a) : Â¬IsMin a :=
  ha.1
#align sup_prime.not_is_min SupPrime.not_isMin

theorem IsMin.not_supIrred (ha : IsMin a) : Â¬SupIrred a := fun h => h.1 ha
#align is_min.not_sup_irred IsMin.not_supIrred

theorem IsMin.not_supPrime (ha : IsMin a) : Â¬SupPrime a := fun h => h.1 ha
#align is_min.not_sup_prime IsMin.not_supPrime

@[simp]
theorem not_supIrred : Â¬SupIrred a â†” IsMin a âˆ¨ âˆƒ b c, b âŠ” c = a âˆ§ b < a âˆ§ c < a := by
  rw [SupIrred, not_and_or]
  -- âŠ¢ (Â¬Â¬IsMin a âˆ¨ Â¬âˆ€ â¦ƒb c : Î±â¦„, b âŠ” c = a â†’ b = a âˆ¨ c = a) â†” IsMin a âˆ¨ âˆƒ b c, b âŠ” â€¦
  push_neg
  -- âŠ¢ (IsMin a âˆ¨ Exists fun â¦ƒbâ¦„ => Exists fun â¦ƒcâ¦„ => b âŠ” c = a âˆ§ b â‰  a âˆ§ c â‰  a) â†”  â€¦
  rw [existsâ‚‚_congr]
  -- âŠ¢ âˆ€ (a_1 b : Î±), a_1 âŠ” b = a âˆ§ a_1 â‰  a âˆ§ b â‰  a â†” a_1 âŠ” b = a âˆ§ a_1 < a âˆ§ b < a
  simp (config := { contextual := true }) [@eq_comm _ _ a]
  -- ğŸ‰ no goals
#align not_sup_irred not_supIrred

@[simp]
theorem not_supPrime : Â¬SupPrime a â†” IsMin a âˆ¨ âˆƒ b c, a â‰¤ b âŠ” c âˆ§ Â¬a â‰¤ b âˆ§ Â¬a â‰¤ c := by
  rw [SupPrime, not_and_or]; push_neg; rfl
  -- âŠ¢ (Â¬Â¬IsMin a âˆ¨ Â¬âˆ€ â¦ƒb c : Î±â¦„, a â‰¤ b âŠ” c â†’ a â‰¤ b âˆ¨ a â‰¤ c) â†” IsMin a âˆ¨ âˆƒ b c, a â‰¤ â€¦
                             -- âŠ¢ (IsMin a âˆ¨ Exists fun â¦ƒbâ¦„ => Exists fun â¦ƒcâ¦„ => a â‰¤ b âŠ” c âˆ§ Â¬a â‰¤ b âˆ§ Â¬a â‰¤ c)  â€¦
                                       -- ğŸ‰ no goals
#align not_sup_prime not_supPrime

protected theorem SupPrime.supIrred : SupPrime a â†’ SupIrred a :=
  And.imp_right fun h b c ha => by simpa [â† ha] using h ha.ge
                                   -- ğŸ‰ no goals
#align sup_prime.sup_irred SupPrime.supIrred

theorem SupPrime.le_sup (ha : SupPrime a) : a â‰¤ b âŠ” c â†” a â‰¤ b âˆ¨ a â‰¤ c :=
  âŸ¨fun h => ha.2 h, fun h => h.elim le_sup_of_le_left le_sup_of_le_rightâŸ©
#align sup_prime.le_sup SupPrime.le_sup

variable [OrderBot Î±] {s : Finset Î¹} {f : Î¹ â†’ Î±}

@[simp]
theorem not_supIrred_bot : Â¬SupIrred (âŠ¥ : Î±) :=
  isMin_bot.not_supIrred
#align not_sup_irred_bot not_supIrred_bot

@[simp]
theorem not_supPrime_bot : Â¬SupPrime (âŠ¥ : Î±) :=
  isMin_bot.not_supPrime
#align not_sup_prime_bot not_supPrime_bot

theorem SupIrred.ne_bot (ha : SupIrred a) : a â‰  âŠ¥ := by rintro rfl; exact not_supIrred_bot ha
                                                        -- âŠ¢ False
                                                                    -- ğŸ‰ no goals
#align sup_irred.ne_bot SupIrred.ne_bot

theorem SupPrime.ne_bot (ha : SupPrime a) : a â‰  âŠ¥ := by rintro rfl; exact not_supPrime_bot ha
                                                        -- âŠ¢ False
                                                                    -- ğŸ‰ no goals
#align sup_prime.ne_bot SupPrime.ne_bot

theorem SupIrred.finset_sup_eq (ha : SupIrred a) (h : s.sup f = a) : âˆƒ i âˆˆ s, f i = a := by
  classical
  induction' s using Finset.induction with i s _ ih
  Â· simpa [ha.ne_bot] using h.symm
  simp only [exists_prop, exists_mem_insert] at ih âŠ¢
  rw [sup_insert] at h
  exact (ha.2 h).imp_right ih
#align sup_irred.finset_sup_eq SupIrred.finset_sup_eq

theorem SupPrime.le_finset_sup (ha : SupPrime a) : a â‰¤ s.sup f â†” âˆƒ i âˆˆ s, a â‰¤ f i := by
  classical
  induction' s using Finset.induction with i s _ ih
  Â· simp [ha.ne_bot]
  Â· simp only [exists_prop, exists_mem_insert, sup_insert, ha.le_sup, ih]
#align sup_prime.le_finset_sup SupPrime.le_finset_sup

variable [WellFoundedLT Î±]

/-- In a well-founded lattice, any element is the supremum of finitely many sup-irreducible
elements. This is the order-theoretic analogue of prime factorisation. -/
theorem exists_supIrred_decomposition (a : Î±) :
    âˆƒ s : Finset Î±, s.sup id = a âˆ§ âˆ€ â¦ƒbâ¦„, b âˆˆ s â†’ SupIrred b := by
  classical
  apply WellFoundedLT.induction a _
  clear a
  rintro a ih
  by_cases ha : SupIrred a
  Â· exact âŸ¨{a}, by simp [ha]âŸ©
  rw [not_supIrred] at ha
  obtain ha | âŸ¨b, c, rfl, hb, hcâŸ© := ha
  Â· exact âŸ¨âˆ…, by simp [ha.eq_bot]âŸ©
  obtain âŸ¨s, rfl, hsâŸ© := ih _ hb
  obtain âŸ¨t, rfl, htâŸ© := ih _ hc
  exact âŸ¨s âˆª t, sup_union, forall_mem_union.2 âŸ¨hs, htâŸ©âŸ©
#align exists_sup_irred_decomposition exists_supIrred_decomposition

end SemilatticeSup

section SemilatticeInf

variable [SemilatticeInf Î±] {a b c : Î±}

/-- An inf-irreducible element is a non-top element which isn't the infimum of anything bigger. -/
def InfIrred (a : Î±) : Prop :=
  Â¬IsMax a âˆ§ âˆ€ â¦ƒb câ¦„, b âŠ“ c = a â†’ b = a âˆ¨ c = a
#align inf_irred InfIrred

/-- An inf-prime element is a non-top element which isn't bigger than the infimum of anything
bigger. -/
def InfPrime (a : Î±) : Prop :=
  Â¬IsMax a âˆ§ âˆ€ â¦ƒb câ¦„, b âŠ“ c â‰¤ a â†’ b â‰¤ a âˆ¨ c â‰¤ a
#align inf_prime InfPrime

@[simp]
theorem IsMax.not_infIrred (ha : IsMax a) : Â¬InfIrred a := fun h => h.1 ha
#align is_max.not_inf_irred IsMax.not_infIrred

@[simp]
theorem IsMax.not_infPrime (ha : IsMax a) : Â¬InfPrime a := fun h => h.1 ha
#align is_max.not_inf_prime IsMax.not_infPrime

@[simp]
theorem not_infIrred : Â¬InfIrred a â†” IsMax a âˆ¨ âˆƒ b c, b âŠ“ c = a âˆ§ a < b âˆ§ a < c :=
  @not_supIrred Î±áµ’áµˆ _ _
#align not_inf_irred not_infIrred

@[simp]
theorem not_infPrime : Â¬InfPrime a â†” IsMax a âˆ¨ âˆƒ b c, b âŠ“ c â‰¤ a âˆ§ Â¬b â‰¤ a âˆ§ Â¬c â‰¤ a :=
  @not_supPrime Î±áµ’áµˆ _ _
#align not_inf_prime not_infPrime

protected theorem InfPrime.infIrred : InfPrime a â†’ InfIrred a :=
  And.imp_right fun h b c ha => by simpa [â† ha] using h ha.le
                                   -- ğŸ‰ no goals
#align inf_prime.inf_irred InfPrime.infIrred

theorem InfPrime.inf_le (ha : InfPrime a) : b âŠ“ c â‰¤ a â†” b â‰¤ a âˆ¨ c â‰¤ a :=
  âŸ¨fun h => ha.2 h, fun h => h.elim inf_le_of_left_le inf_le_of_right_leâŸ©
#align inf_prime.inf_le InfPrime.inf_le

variable [OrderTop Î±] {s : Finset Î¹} {f : Î¹ â†’ Î±}

-- @[simp] Porting note: simp can prove this.
theorem not_infIrred_top : Â¬InfIrred (âŠ¤ : Î±) :=
  isMax_top.not_infIrred
#align not_inf_irred_top not_infIrred_top

-- @[simp] Porting note: simp can prove this.
theorem not_infPrime_top : Â¬InfPrime (âŠ¤ : Î±) :=
  isMax_top.not_infPrime
#align not_inf_prime_top not_infPrime_top

theorem InfIrred.ne_top (ha : InfIrred a) : a â‰  âŠ¤ := by rintro rfl; exact not_infIrred_top ha
                                                        -- âŠ¢ False
                                                                    -- ğŸ‰ no goals
#align inf_irred.ne_top InfIrred.ne_top

theorem InfPrime.ne_top (ha : InfPrime a) : a â‰  âŠ¤ := by rintro rfl; exact not_infPrime_top ha
                                                        -- âŠ¢ False
                                                                    -- ğŸ‰ no goals
#align inf_prime.ne_top InfPrime.ne_top

theorem InfIrred.finset_inf_eq : InfIrred a â†’ s.inf f = a â†’ âˆƒ i âˆˆ s, f i = a :=
  @SupIrred.finset_sup_eq _ Î±áµ’áµˆ _ _ _ _ _
#align inf_irred.finset_inf_eq InfIrred.finset_inf_eq

theorem InfPrime.finset_inf_le (ha : InfPrime a) : s.inf f â‰¤ a â†” âˆƒ i âˆˆ s, f i â‰¤ a :=
  @SupPrime.le_finset_sup _ Î±áµ’áµˆ _ _ _ _ _ ha
#align inf_prime.finset_inf_le InfPrime.finset_inf_le

variable [WellFoundedGT Î±]

/-- In a cowell-founded lattice, any element is the infimum of finitely many inf-irreducible
elements. This is the order-theoretic analogue of prime factorisation. -/
theorem exists_infIrred_decomposition (a : Î±) :
    âˆƒ s : Finset Î±, s.inf id = a âˆ§ âˆ€ â¦ƒbâ¦„, b âˆˆ s â†’ InfIrred b :=
  @exists_supIrred_decomposition Î±áµ’áµˆ _ _ _ _
#align exists_inf_irred_decomposition exists_infIrred_decomposition

end SemilatticeInf

section SemilatticeSup

variable [SemilatticeSup Î±]

@[simp]
theorem infIrred_toDual {a : Î±} : InfIrred (toDual a) â†” SupIrred a :=
  Iff.rfl
#align inf_irred_to_dual infIrred_toDual

@[simp]
theorem infPrime_toDual {a : Î±} : InfPrime (toDual a) â†” SupPrime a :=
  Iff.rfl
#align inf_prime_to_dual infPrime_toDual

@[simp]
theorem supIrred_ofDual {a : Î±áµ’áµˆ} : SupIrred (ofDual a) â†” InfIrred a :=
  Iff.rfl
#align sup_irred_of_dual supIrred_ofDual

@[simp]
theorem supPrime_ofDual {a : Î±áµ’áµˆ} : SupPrime (ofDual a) â†” InfPrime a :=
  Iff.rfl
#align sup_prime_of_dual supPrime_ofDual

alias âŸ¨_, SupIrred.dualâŸ© := infIrred_toDual
#align sup_irred.dual SupIrred.dual

alias âŸ¨_, SupPrime.dualâŸ© := infPrime_toDual
#align sup_prime.dual SupPrime.dual

alias âŸ¨_, InfIrred.ofDualâŸ© := supIrred_ofDual
#align inf_irred.of_dual InfIrred.ofDual

alias âŸ¨_, InfPrime.ofDualâŸ© := supPrime_ofDual
#align inf_prime.of_dual InfPrime.ofDual

end SemilatticeSup

section SemilatticeInf

variable [SemilatticeInf Î±]

@[simp]
theorem supIrred_toDual {a : Î±} : SupIrred (toDual a) â†” InfIrred a :=
  Iff.rfl
#align sup_irred_to_dual supIrred_toDual

@[simp]
theorem supPrime_toDual {a : Î±} : SupPrime (toDual a) â†” InfPrime a :=
  Iff.rfl
#align sup_prime_to_dual supPrime_toDual

@[simp]
theorem infIrred_ofDual {a : Î±áµ’áµˆ} : InfIrred (ofDual a) â†” SupIrred a :=
  Iff.rfl
#align inf_irred_of_dual infIrred_ofDual

@[simp]
theorem infPrime_ofDual {a : Î±áµ’áµˆ} : InfPrime (ofDual a) â†” SupPrime a :=
  Iff.rfl
#align inf_prime_of_dual infPrime_ofDual

alias âŸ¨_, InfIrred.dualâŸ© := supIrred_toDual
#align inf_irred.dual InfIrred.dual

alias âŸ¨_, InfPrime.dualâŸ© := supPrime_toDual
#align inf_prime.dual InfPrime.dual

alias âŸ¨_, SupIrred.ofDualâŸ© := infIrred_ofDual
#align sup_irred.of_dual SupIrred.ofDual

alias âŸ¨_, SupPrime.ofDualâŸ© := infPrime_ofDual
#align sup_prime.of_dual SupPrime.ofDual

end SemilatticeInf

section DistribLattice

variable [DistribLattice Î±] {a b c : Î±}

@[simp]
theorem supPrime_iff_supIrred : SupPrime a â†” SupIrred a :=
  âŸ¨SupPrime.supIrred,
    And.imp_right fun h b c => by simp_rw [â† inf_eq_left, inf_sup_left]; exact @h _ _âŸ©
                                  -- âŠ¢ a âŠ“ b âŠ” a âŠ“ c = a â†’ a âŠ“ b = a âˆ¨ a âŠ“ c = a
                                                                         -- ğŸ‰ no goals
#align sup_prime_iff_sup_irred supPrime_iff_supIrred

@[simp]
theorem infPrime_iff_infIrred : InfPrime a â†” InfIrred a :=
  âŸ¨InfPrime.infIrred,
    And.imp_right fun h b c => by simp_rw [â† sup_eq_left, sup_inf_left]; exact @h _ _âŸ©
                                  -- âŠ¢ (a âŠ” b) âŠ“ (a âŠ” c) = a â†’ a âŠ” b = a âˆ¨ a âŠ” c = a
                                                                         -- ğŸ‰ no goals
#align inf_prime_iff_inf_irred infPrime_iff_infIrred

alias âŸ¨_, SupIrred.supPrimeâŸ© := supPrime_iff_supIrred
#align sup_irred.sup_prime SupIrred.supPrime

alias âŸ¨_, InfIrred.infPrimeâŸ© := infPrime_iff_infIrred
#align inf_irred.inf_prime InfIrred.infPrime

-- porting note: was attribute [protected] SupIrred.supPrime InfIrred.infPrime

end DistribLattice

section LinearOrder

variable [LinearOrder Î±] {a : Î±}

-- @[simp] Porting note: simp can prove this
theorem supPrime_iff_not_isMin : SupPrime a â†” Â¬IsMin a :=
  and_iff_left <| by simp
                     -- ğŸ‰ no goals
#align sup_prime_iff_not_is_min supPrime_iff_not_isMin

-- @[simp] Porting note: simp can prove this
theorem infPrime_iff_not_isMax : InfPrime a â†” Â¬IsMax a :=
  and_iff_left <| by simp
                     -- ğŸ‰ no goals
#align inf_prime_iff_not_is_max infPrime_iff_not_isMax

@[simp]
theorem supIrred_iff_not_isMin : SupIrred a â†” Â¬IsMin a :=
  and_iff_left fun _ _ => by simpa only [sup_eq_max, max_eq_iff] using Or.imp And.left And.left
                             -- ğŸ‰ no goals
#align sup_irred_iff_not_is_min supIrred_iff_not_isMin

@[simp]
theorem infIrred_iff_not_isMax : InfIrred a â†” Â¬IsMax a :=
  and_iff_left fun _ _ => by simpa only [inf_eq_min, min_eq_iff] using Or.imp And.left And.left
                             -- ğŸ‰ no goals
#align inf_irred_iff_not_is_max infIrred_iff_not_isMax

end LinearOrder
