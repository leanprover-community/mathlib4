/-
Copyright (c) 2022 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies
-/
import Mathlib.Data.Set.Lattice

#align_import order.concept from "leanprover-community/mathlib"@"1e05171a5e8cf18d98d9cf7b207540acb044acae"

/-!
# Formal concept analysis

This file defines concept lattices. A concept of a relation `r : Œ± ‚Üí Œ≤ ‚Üí Prop` is a pair of sets
`s : Set Œ±` and `t : Set Œ≤` such that `s` is the set of all `a : Œ±` that are related to all elements
of `t`, and `t` is the set of all `b : Œ≤` that are related to all elements of `s`.

Ordering the concepts of a relation `r` by inclusion on the first component gives rise to a
*concept lattice*. Every concept lattice is complete and in fact every complete lattice arises as
the concept lattice of its `‚â§`.

## Implementation notes

Concept lattices are usually defined from a *context*, that is the triple `(Œ±, Œ≤, r)`, but the type
of `r` determines `Œ±` and `Œ≤` already, so we do not define contexts as a separate object.

## TODO

Prove the fundamental theorem of concept lattices.

## References

* [Davey, Priestley *Introduction to Lattices and Order*][davey_priestley]

## Tags

concept, formal concept analysis, intent, extend, attribute
-/


open Function OrderDual Set

variable {Œπ : Sort*} {Œ± Œ≤ Œ≥ : Type*} {Œ∫ : Œπ ‚Üí Sort*} (r : Œ± ‚Üí Œ≤ ‚Üí Prop) {s s‚ÇÅ s‚ÇÇ : Set Œ±}
  {t t‚ÇÅ t‚ÇÇ : Set Œ≤}

/-! ### Intent and extent -/


/-- The intent closure of `s : Set Œ±` along a relation `r : Œ± ‚Üí Œ≤ ‚Üí Prop` is the set of all elements
which `r` relates to all elements of `s`. -/
def intentClosure (s : Set Œ±) : Set Œ≤ :=
  { b | ‚àÄ ‚¶Éa‚¶Ñ, a ‚àà s ‚Üí r a b }
#align intent_closure intentClosure

/-- The extent closure of `t : Set Œ≤` along a relation `r : Œ± ‚Üí Œ≤ ‚Üí Prop` is the set of all elements
which `r` relates to all elements of `t`. -/
def extentClosure (t : Set Œ≤) : Set Œ± :=
  { a | ‚àÄ ‚¶Éb‚¶Ñ, b ‚àà t ‚Üí r a b }
#align extent_closure extentClosure

variable {r}

theorem subset_intentClosure_iff_subset_extentClosure :
    t ‚äÜ intentClosure r s ‚Üî s ‚äÜ extentClosure r t :=
  ‚ü®fun h _ ha _ hb => h hb ha, fun h _ hb _ ha => h ha hb‚ü©
#align subset_intent_closure_iff_subset_extent_closure subset_intentClosure_iff_subset_extentClosure

variable (r)

theorem gc_intentClosure_extentClosure :
    GaloisConnection (toDual ‚àò intentClosure r) (extentClosure r ‚àò ofDual) := fun _ _ =>
  subset_intentClosure_iff_subset_extentClosure
#align gc_intent_closure_extent_closure gc_intentClosure_extentClosure

theorem intentClosure_swap (t : Set Œ≤) : intentClosure (swap r) t = extentClosure r t :=
  rfl
#align intent_closure_swap intentClosure_swap

theorem extentClosure_swap (s : Set Œ±) : extentClosure (swap r) s = intentClosure r s :=
  rfl
#align extent_closure_swap extentClosure_swap

@[simp]
theorem intentClosure_empty : intentClosure r ‚àÖ = univ :=
  eq_univ_of_forall fun _ _ => False.elim
#align intent_closure_empty intentClosure_empty

@[simp]
theorem extentClosure_empty : extentClosure r ‚àÖ = univ :=
  intentClosure_empty _
#align extent_closure_empty extentClosure_empty

@[simp]
theorem intentClosure_union (s‚ÇÅ s‚ÇÇ : Set Œ±) :
    intentClosure r (s‚ÇÅ ‚à™ s‚ÇÇ) = intentClosure r s‚ÇÅ ‚à© intentClosure r s‚ÇÇ :=
  Set.ext fun _ => ball_or_left
#align intent_closure_union intentClosure_union

@[simp]
theorem extentClosure_union (t‚ÇÅ t‚ÇÇ : Set Œ≤) :
    extentClosure r (t‚ÇÅ ‚à™ t‚ÇÇ) = extentClosure r t‚ÇÅ ‚à© extentClosure r t‚ÇÇ :=
  intentClosure_union _ _ _
#align extent_closure_union extentClosure_union

@[simp]
theorem intentClosure_iUnion (f : Œπ ‚Üí Set Œ±) :
    intentClosure r (‚ãÉ i, f i) = ‚ãÇ i, intentClosure r (f i) :=
  (gc_intentClosure_extentClosure r).l_iSup
#align intent_closure_Union intentClosure_iUnion

@[simp]
theorem extentClosure_iUnion (f : Œπ ‚Üí Set Œ≤) :
    extentClosure r (‚ãÉ i, f i) = ‚ãÇ i, extentClosure r (f i) :=
  intentClosure_iUnion _ _
#align extent_closure_Union extentClosure_iUnion

/- ./././Mathport/Syntax/Translate/Expr.lean:107:6: warning: expanding binder group (i j) -/
/- ./././Mathport/Syntax/Translate/Expr.lean:107:6: warning: expanding binder group (i j) -/
-- Porting note: Can be proved by simp. so not marked as @[simp]
-- @[simp]
theorem intentClosure_iUnion‚ÇÇ (f : ‚àÄ i, Œ∫ i ‚Üí Set Œ±) :
    intentClosure r (‚ãÉ (i) (j), f i j) = ‚ãÇ (i) (j), intentClosure r (f i j) :=
  (gc_intentClosure_extentClosure r).l_iSup‚ÇÇ
#align intent_closure_Union‚ÇÇ intentClosure_iUnion‚ÇÇ

/- ./././Mathport/Syntax/Translate/Expr.lean:107:6: warning: expanding binder group (i j) -/
/- ./././Mathport/Syntax/Translate/Expr.lean:107:6: warning: expanding binder group (i j) -/
-- Porting note: Can be proved by simp. so not marked as @[simp]
-- @[simp]
theorem extentClosure_Union‚ÇÇ (f : ‚àÄ i, Œ∫ i ‚Üí Set Œ≤) :
    extentClosure r (‚ãÉ (i) (j), f i j) = ‚ãÇ (i) (j), extentClosure r (f i j) :=
  intentClosure_iUnion‚ÇÇ _ _
#align extent_closure_Union‚ÇÇ extentClosure_Union‚ÇÇ

theorem subset_extentClosure_intentClosure (s : Set Œ±) :
    s ‚äÜ extentClosure r (intentClosure r s) :=
  (gc_intentClosure_extentClosure r).le_u_l _
#align subset_extent_closure_intent_closure subset_extentClosure_intentClosure

theorem subset_intentClosure_extentClosure (t : Set Œ≤) :
    t ‚äÜ intentClosure r (extentClosure r t) :=
  subset_extentClosure_intentClosure _ t
#align subset_intent_closure_extent_closure subset_intentClosure_extentClosure

@[simp]
theorem intentClosure_extentClosure_intentClosure (s : Set Œ±) :
    intentClosure r (extentClosure r <| intentClosure r s) = intentClosure r s :=
  (gc_intentClosure_extentClosure r).l_u_l_eq_l _
#align intent_closure_extent_closure_intent_closure intentClosure_extentClosure_intentClosure

@[simp]
theorem extentClosure_intentClosure_extentClosure (t : Set Œ≤) :
    extentClosure r (intentClosure r <| extentClosure r t) = extentClosure r t :=
  intentClosure_extentClosure_intentClosure _ t
#align extent_closure_intent_closure_extent_closure extentClosure_intentClosure_extentClosure

theorem intentClosure_anti : Antitone (intentClosure r) :=
  (gc_intentClosure_extentClosure r).monotone_l
#align intent_closure_anti intentClosure_anti

theorem extentClosure_anti : Antitone (extentClosure r) :=
  intentClosure_anti _
#align extent_closure_anti extentClosure_anti

/-! ### Concepts -/


variable (Œ± Œ≤)

/-- The formal concepts of a relation. A concept of `r : Œ± ‚Üí Œ≤ ‚Üí Prop` is a pair of sets `s`, `t`
such that `s` is the set of all elements that are `r`-related to all of `t` and `t` is the set of
all elements that are `r`-related to all of `s`. -/
structure Concept extends Set Œ± √ó Set Œ≤ where
  /-- The axiom of a `Concept` stating that the closure of the first set is the second set. -/
  closure_fst : intentClosure r fst = snd
  /-- The axiom of a `Concept` stating that the closure of the second set is the first set. -/
  closure_snd : extentClosure r snd = fst
#align concept Concept

initialize_simps_projections Concept (+toProd, -fst, -snd)

namespace Concept

variable {r Œ± Œ≤} {c d : Concept Œ± Œ≤ r}

attribute [simp] closure_fst closure_snd

@[ext]
theorem ext (h : c.fst = d.fst) : c = d := by
  obtain ‚ü®‚ü®s‚ÇÅ, t‚ÇÅ‚ü©, h‚ÇÅ, _‚ü© := c
  -- ‚ä¢ { toProd := (s‚ÇÅ, t‚ÇÅ), closure_fst := h‚ÇÅ, closure_snd := closure_snd‚úù } = d
  obtain ‚ü®‚ü®s‚ÇÇ, t‚ÇÇ‚ü©, h‚ÇÇ, _‚ü© := d
  -- ‚ä¢ { toProd := (s‚ÇÅ, t‚ÇÅ), closure_fst := h‚ÇÅ, closure_snd := closure_snd‚úù¬π } = {  ‚Ä¶
  dsimp at h‚ÇÅ h‚ÇÇ h
  -- ‚ä¢ { toProd := (s‚ÇÅ, t‚ÇÅ), closure_fst := h‚ÇÅ, closure_snd := closure_snd‚úù¬π } = {  ‚Ä¶
  substs h h‚ÇÅ h‚ÇÇ
  -- ‚ä¢ { toProd := (s‚ÇÅ, intentClosure r s‚ÇÅ), closure_fst := (_ : intentClosure r s‚ÇÅ ‚Ä¶
  rfl
  -- üéâ no goals
#align concept.ext Concept.ext

theorem ext' (h : c.snd = d.snd) : c = d := by
  obtain ‚ü®‚ü®s‚ÇÅ, t‚ÇÅ‚ü©, _, h‚ÇÅ‚ü© := c
  -- ‚ä¢ { toProd := (s‚ÇÅ, t‚ÇÅ), closure_fst := closure_fst‚úù, closure_snd := h‚ÇÅ } = d
  obtain ‚ü®‚ü®s‚ÇÇ, t‚ÇÇ‚ü©, _, h‚ÇÇ‚ü© := d
  -- ‚ä¢ { toProd := (s‚ÇÅ, t‚ÇÅ), closure_fst := closure_fst‚úù¬π, closure_snd := h‚ÇÅ } = {  ‚Ä¶
  dsimp at h‚ÇÅ h‚ÇÇ h
  -- ‚ä¢ { toProd := (s‚ÇÅ, t‚ÇÅ), closure_fst := closure_fst‚úù¬π, closure_snd := h‚ÇÅ } = {  ‚Ä¶
  substs h h‚ÇÅ h‚ÇÇ
  -- ‚ä¢ { toProd := (extentClosure r t‚ÇÅ, t‚ÇÅ), closure_fst := closure_fst‚úù¬π, closure_ ‚Ä¶
  rfl
  -- üéâ no goals
#align concept.ext' Concept.ext'

theorem fst_injective : Injective fun c : Concept Œ± Œ≤ r => c.fst := fun _ _ => ext
#align concept.fst_injective Concept.fst_injective

theorem snd_injective : Injective fun c : Concept Œ± Œ≤ r => c.snd := fun _ _ => ext'
#align concept.snd_injective Concept.snd_injective

instance instSupConcept : Sup (Concept Œ± Œ≤ r) :=
  ‚ü®fun c d =>
    { fst := extentClosure r (c.snd ‚à© d.snd)
      snd := c.snd ‚à© d.snd
      closure_fst := by
        rw [‚Üê c.closure_fst, ‚Üê d.closure_fst, ‚Üê intentClosure_union,
          intentClosure_extentClosure_intentClosure]
      closure_snd := rfl }‚ü©

instance instInfConcept : Inf (Concept Œ± Œ≤ r) :=
  ‚ü®fun c d =>
    { fst := c.fst ‚à© d.fst
      snd := intentClosure r (c.fst ‚à© d.fst)
      closure_fst := rfl
      closure_snd := by
        rw [‚Üê c.closure_snd, ‚Üê d.closure_snd, ‚Üê extentClosure_union,
          extentClosure_intentClosure_extentClosure] }‚ü©

instance instSemilatticeInfConcept : SemilatticeInf (Concept Œ± Œ≤ r) :=
  (fst_injective.semilatticeInf _) fun _ _ => rfl

@[simp]
theorem fst_subset_fst_iff : c.fst ‚äÜ d.fst ‚Üî c ‚â§ d :=
  Iff.rfl
#align concept.fst_subset_fst_iff Concept.fst_subset_fst_iff

@[simp]
theorem fst_ssubset_fst_iff : c.fst ‚äÇ d.fst ‚Üî c < d :=
  Iff.rfl
#align concept.fst_ssubset_fst_iff Concept.fst_ssubset_fst_iff

@[simp]
theorem snd_subset_snd_iff : c.snd ‚äÜ d.snd ‚Üî d ‚â§ c := by
  refine' ‚ü®fun h => _, fun h => _‚ü©
  -- ‚ä¢ d ‚â§ c
  ¬∑ rw [‚Üê fst_subset_fst_iff, ‚Üê c.closure_snd, ‚Üê d.closure_snd]
    -- ‚ä¢ extentClosure r d.snd ‚äÜ extentClosure r c.snd
    exact extentClosure_anti _ h
    -- üéâ no goals
  ¬∑ rw [‚Üê c.closure_fst, ‚Üê d.closure_fst]
    -- ‚ä¢ intentClosure r c.fst ‚äÜ intentClosure r d.fst
    exact intentClosure_anti _ h
    -- üéâ no goals
#align concept.snd_subset_snd_iff Concept.snd_subset_snd_iff

@[simp]
theorem snd_ssubset_snd_iff : c.snd ‚äÇ d.snd ‚Üî d < c := by
  rw [ssubset_iff_subset_not_subset, lt_iff_le_not_le, snd_subset_snd_iff, snd_subset_snd_iff]
  -- üéâ no goals
#align concept.snd_ssubset_snd_iff Concept.snd_ssubset_snd_iff

theorem strictMono_fst : StrictMono (Prod.fst ‚àò toProd : Concept Œ± Œ≤ r ‚Üí Set Œ±) := fun _ _ =>
  fst_ssubset_fst_iff.2
#align concept.strict_mono_fst Concept.strictMono_fst

theorem strictAnti_snd : StrictAnti (Prod.snd ‚àò toProd : Concept Œ± Œ≤ r ‚Üí Set Œ≤) := fun _ _ =>
  snd_ssubset_snd_iff.2
#align concept.strict_anti_snd Concept.strictAnti_snd

instance instLatticeConcept : Lattice (Concept Œ± Œ≤ r) :=
  { Concept.instSemilatticeInfConcept with
    sup := (¬∑ ‚äî ¬∑)
    le_sup_left := fun c d => snd_subset_snd_iff.1 <| inter_subset_left _ _
    le_sup_right := fun c d => snd_subset_snd_iff.1 <| inter_subset_right _ _
    sup_le := fun c d e => by
      simp_rw [‚Üê snd_subset_snd_iff]
      -- ‚ä¢ e.snd ‚äÜ c.snd ‚Üí e.snd ‚äÜ d.snd ‚Üí e.snd ‚äÜ (c ‚äî d).snd
      exact subset_inter }
      -- üéâ no goals

instance instBoundedOrderConcept : BoundedOrder (Concept Œ± Œ≤ r) where
  top := ‚ü®‚ü®univ, intentClosure r univ‚ü©, rfl, eq_univ_of_forall fun _ _ hb => hb trivial‚ü©
  le_top _ := subset_univ _
  bot := ‚ü®‚ü®extentClosure r univ, univ‚ü©, eq_univ_of_forall fun _ _ ha => ha trivial, rfl‚ü©
  bot_le _ := snd_subset_snd_iff.1 <| subset_univ _

instance : SupSet (Concept Œ± Œ≤ r) :=
  ‚ü®fun S =>
    { fst := extentClosure r (‚ãÇ c ‚àà S, (c : Concept _ _ _).snd)
      snd := ‚ãÇ c ‚àà S, (c : Concept _ _ _).snd
      closure_fst := by
        simp_rw [‚Üê closure_fst, ‚Üê intentClosure_iUnion‚ÇÇ,
          intentClosure_extentClosure_intentClosure]
      closure_snd := rfl }‚ü©

instance : InfSet (Concept Œ± Œ≤ r) :=
  ‚ü®fun S =>
    { fst := ‚ãÇ c ‚àà S, (c : Concept _ _ _).fst
      snd := intentClosure r (‚ãÇ c ‚àà S, (c : Concept _ _ _).fst)
      closure_fst := rfl
      closure_snd := by
        simp_rw [‚Üê closure_snd, ‚Üê extentClosure_Union‚ÇÇ,
          extentClosure_intentClosure_extentClosure] }‚ü©

instance : CompleteLattice (Concept Œ± Œ≤ r) :=
  { Concept.instLatticeConcept,
    Concept.instBoundedOrderConcept with
    sup := Concept.instSupConcept.sup
    le_sSup := fun _ _ hc => snd_subset_snd_iff.1 <| biInter_subset_of_mem hc
    sSup_le := fun _ _ hc =>
      snd_subset_snd_iff.1 <| subset_iInter‚ÇÇ fun d hd => snd_subset_snd_iff.2 <| hc d hd
    inf := Concept.instInfConcept.inf
    sInf_le := fun _ _ => biInter_subset_of_mem
    le_sInf := fun _ _ => subset_iInter‚ÇÇ }

@[simp]
theorem top_fst : (‚ä§ : Concept Œ± Œ≤ r).fst = univ :=
  rfl
#align concept.top_fst Concept.top_fst

@[simp]
theorem top_snd : (‚ä§ : Concept Œ± Œ≤ r).snd = intentClosure r univ :=
  rfl
#align concept.top_snd Concept.top_snd

@[simp]
theorem bot_fst : (‚ä• : Concept Œ± Œ≤ r).fst = extentClosure r univ :=
  rfl
#align concept.bot_fst Concept.bot_fst

@[simp]
theorem bot_snd : (‚ä• : Concept Œ± Œ≤ r).snd = univ :=
  rfl
#align concept.bot_snd Concept.bot_snd

@[simp]
theorem sup_fst (c d : Concept Œ± Œ≤ r) : (c ‚äî d).fst = extentClosure r (c.snd ‚à© d.snd) :=
  rfl
#align concept.sup_fst Concept.sup_fst

@[simp]
theorem sup_snd (c d : Concept Œ± Œ≤ r) : (c ‚äî d).snd = c.snd ‚à© d.snd :=
  rfl
#align concept.sup_snd Concept.sup_snd

@[simp]
theorem inf_fst (c d : Concept Œ± Œ≤ r) : (c ‚äì d).fst = c.fst ‚à© d.fst :=
  rfl
#align concept.inf_fst Concept.inf_fst

@[simp]
theorem inf_snd (c d : Concept Œ± Œ≤ r) : (c ‚äì d).snd = intentClosure r (c.fst ‚à© d.fst) :=
  rfl
#align concept.inf_snd Concept.inf_snd

@[simp]
theorem sSup_fst (S : Set (Concept Œ± Œ≤ r)) :
    (sSup S).fst = extentClosure r (‚ãÇ c ‚àà S, (c : Concept _ _ _).snd) :=
  rfl
#align concept.Sup_fst Concept.sSup_fst

@[simp]
theorem sSup_snd (S : Set (Concept Œ± Œ≤ r)) : (sSup S).snd = ‚ãÇ c ‚àà S, (c : Concept _ _ _).snd :=
  rfl
#align concept.Sup_snd Concept.sSup_snd

@[simp]
theorem sInf_fst (S : Set (Concept Œ± Œ≤ r)) : (sInf S).fst = ‚ãÇ c ‚àà S, (c : Concept _ _ _).fst :=
  rfl
#align concept.Inf_fst Concept.sInf_fst

@[simp]
theorem sInf_snd (S : Set (Concept Œ± Œ≤ r)) :
    (sInf S).snd = intentClosure r (‚ãÇ c ‚àà S, (c : Concept _ _ _).fst) :=
  rfl
#align concept.Inf_snd Concept.sInf_snd

instance : Inhabited (Concept Œ± Œ≤ r) :=
  ‚ü®‚ä•‚ü©

/-- Swap the sets of a concept to make it a concept of the dual context. -/
@[simps]
def swap (c : Concept Œ± Œ≤ r) : Concept Œ≤ Œ± (swap r) :=
  ‚ü®c.toProd.swap, c.closure_snd, c.closure_fst‚ü©
#align concept.swap Concept.swap
#align concept.swap_to_prod Concept.swap_toProd

@[simp]
theorem swap_swap (c : Concept Œ± Œ≤ r) : c.swap.swap = c :=
  ext rfl
#align concept.swap_swap Concept.swap_swap

@[simp]
theorem swap_le_swap_iff : c.swap ‚â§ d.swap ‚Üî d ‚â§ c :=
  snd_subset_snd_iff
#align concept.swap_le_swap_iff Concept.swap_le_swap_iff

@[simp]
theorem swap_lt_swap_iff : c.swap < d.swap ‚Üî d < c :=
  snd_ssubset_snd_iff
#align concept.swap_lt_swap_iff Concept.swap_lt_swap_iff

/-- The dual of a concept lattice is isomorphic to the concept lattice of the dual context. -/
@[simps]
def swapEquiv : (Concept Œ± Œ≤ r)·µí·µà ‚âÉo Concept Œ≤ Œ± (Function.swap r) where
  toFun := swap ‚àò ofDual
  invFun := toDual ‚àò swap
  left_inv := swap_swap
  right_inv := swap_swap
  map_rel_iff' := swap_le_swap_iff
#align concept.swap_equiv Concept.swapEquiv
#align concept.swap_equiv_symm_apply Concept.swapEquiv_symm_apply
#align concept.swap_equiv_apply Concept.swapEquiv_apply

end Concept
