/-
Copyright (c) 2020 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Mario Carneiro
-/
import Mathlib.Data.Set.Image

#align_import order.well_founded from "leanprover-community/mathlib"@"2c84c2c5496117349007d97104e7bbb471381592"

/-!
# Well-founded relations

A relation is well-founded if it can be used for induction: for each `x`, `(‚àÄ y, r y x ‚Üí P y) ‚Üí P x`
implies `P x`. Well-founded relations can be used for induction and recursion, including
construction of fixed points in the space of dependent functions `Œ† x : Œ± , Œ≤ x`.

The predicate `WellFounded` is defined in the core library. In this file we prove some extra lemmas
and provide a few new definitions: `WellFounded.min`, `WellFounded.sup`, and `WellFounded.succ`,
and an induction principle `WellFounded.induction_bot`.
-/


variable {Œ± Œ≤ Œ≥ : Type*}

namespace WellFounded

variable {r r' : Œ± ‚Üí Œ± ‚Üí Prop}

#align well_founded_relation.r WellFoundedRelation.rel

protected theorem isAsymm (h : WellFounded r) : IsAsymm Œ± r := ‚ü®h.asymmetric‚ü©
#align well_founded.is_asymm WellFounded.isAsymm

protected theorem isIrrefl (h : WellFounded r) : IsIrrefl Œ± r := @IsAsymm.isIrrefl Œ± r h.isAsymm
#align well_founded.is_irrefl WellFounded.isIrrefl

instance [WellFoundedRelation Œ±] : IsAsymm Œ± WellFoundedRelation.rel :=
  WellFoundedRelation.wf.isAsymm

instance : IsIrrefl Œ± WellFoundedRelation.rel := IsAsymm.isIrrefl

theorem mono (hr : WellFounded r) (h : ‚àÄ a b, r' a b ‚Üí r a b) : WellFounded r' :=
  Subrelation.wf (h _ _) hr
#align well_founded.mono WellFounded.mono

theorem onFun {Œ± Œ≤ : Sort*} {r : Œ≤ ‚Üí Œ≤ ‚Üí Prop} {f : Œ± ‚Üí Œ≤} :
    WellFounded r ‚Üí WellFounded (r on f) :=
  InvImage.wf _
#align well_founded.on_fun WellFounded.onFun

/-- If `r` is a well-founded relation, then any nonempty set has a minimal element
with respect to `r`. -/
theorem has_min {Œ±} {r : Œ± ‚Üí Œ± ‚Üí Prop} (H : WellFounded r) (s : Set Œ±) :
    s.Nonempty ‚Üí ‚àÉ a ‚àà s, ‚àÄ x ‚àà s, ¬¨r x a
  | ‚ü®a, ha‚ü© => show ‚àÉ b ‚àà s, ‚àÄ x ‚àà s, ¬¨r x b from
    Acc.recOn (H.apply a) (fun x _ IH =>
        not_imp_not.1 fun hne hx => hne <| ‚ü®x, hx, fun y hy hyx => hne <| IH y hyx hy‚ü©)
      ha
#align well_founded.has_min WellFounded.has_min

/-- A minimal element of a nonempty set in a well-founded order.

If you're working with a nonempty linear order, consider defining a
`ConditionallyCompleteLinearOrderBot` instance via
`WellFounded.conditionallyCompleteLinearOrderWithBot` and using `Inf` instead. -/
noncomputable def min {r : Œ± ‚Üí Œ± ‚Üí Prop} (H : WellFounded r) (s : Set Œ±) (h : s.Nonempty) : Œ± :=
  Classical.choose (H.has_min s h)
#align well_founded.min WellFounded.min

theorem min_mem {r : Œ± ‚Üí Œ± ‚Üí Prop} (H : WellFounded r) (s : Set Œ±) (h : s.Nonempty) :
    H.min s h ‚àà s :=
  let ‚ü®h, _‚ü© := Classical.choose_spec (H.has_min s h)
  h
#align well_founded.min_mem WellFounded.min_mem

theorem not_lt_min {r : Œ± ‚Üí Œ± ‚Üí Prop} (H : WellFounded r) (s : Set Œ±) (h : s.Nonempty) {x}
    (hx : x ‚àà s) : ¬¨r x (H.min s h) :=
  let ‚ü®_, h'‚ü© := Classical.choose_spec (H.has_min s h)
  h' _ hx
#align well_founded.not_lt_min WellFounded.not_lt_min

theorem wellFounded_iff_has_min {r : Œ± ‚Üí Œ± ‚Üí Prop} :
    WellFounded r ‚Üî ‚àÄ s : Set Œ±, s.Nonempty ‚Üí ‚àÉ m ‚àà s, ‚àÄ x ‚àà s, ¬¨r x m := by
  refine ‚ü®fun h => h.has_min, fun h => ‚ü®fun x => ?_‚ü©‚ü©
  -- ‚ä¢ Acc r x
  by_contra hx
  -- ‚ä¢ False
  obtain ‚ü®m, hm, hm'‚ü© := h {x | ¬¨Acc r x} ‚ü®x, hx‚ü©
  -- ‚ä¢ False
  refine' hm ‚ü®_, fun y hy => _‚ü©
  -- ‚ä¢ Acc r y
  by_contra hy'
  -- ‚ä¢ False
  exact hm' y hy' hy
  -- üéâ no goals
#align well_founded.well_founded_iff_has_min WellFounded.wellFounded_iff_has_min

open Set

/-- The supremum of a bounded, well-founded order -/
protected noncomputable def sup {r : Œ± ‚Üí Œ± ‚Üí Prop} (wf : WellFounded r) (s : Set Œ±)
    (h : Bounded r s) : Œ± :=
  wf.min { x | ‚àÄ a ‚àà s, r a x } h
#align well_founded.sup WellFounded.sup

protected theorem lt_sup {r : Œ± ‚Üí Œ± ‚Üí Prop} (wf : WellFounded r) {s : Set Œ±} (h : Bounded r s) {x}
    (hx : x ‚àà s) : r x (wf.sup s h) :=
  min_mem wf { x | ‚àÄ a ‚àà s, r a x } h x hx
#align well_founded.lt_sup WellFounded.lt_sup

section

open Classical

/-- A successor of an element `x` in a well-founded order is a minimal element `y` such that
`x < y` if one exists. Otherwise it is `x` itself. -/
protected noncomputable def succ {r : Œ± ‚Üí Œ± ‚Üí Prop} (wf : WellFounded r) (x : Œ±) : Œ± :=
  if h : ‚àÉ y, r x y then wf.min { y | r x y } h else x
#align well_founded.succ WellFounded.succ

protected theorem lt_succ {r : Œ± ‚Üí Œ± ‚Üí Prop} (wf : WellFounded r) {x : Œ±} (h : ‚àÉ y, r x y) :
    r x (wf.succ x) := by
  rw [WellFounded.succ, dif_pos h]
  -- ‚ä¢ r x (min wf {y | r x y} h)
  apply min_mem
  -- üéâ no goals
#align well_founded.lt_succ WellFounded.lt_succ

end

protected theorem lt_succ_iff {r : Œ± ‚Üí Œ± ‚Üí Prop} [wo : IsWellOrder Œ± r] {x : Œ±} (h : ‚àÉ y, r x y)
    (y : Œ±) : r y (wo.wf.succ x) ‚Üî r y x ‚à® y = x := by
  constructor
  -- ‚ä¢ r y (WellFounded.succ (_ : WellFounded r) x) ‚Üí r y x ‚à® y = x
  ¬∑ intro h'
    -- ‚ä¢ r y x ‚à® y = x
    have : ¬¨r x y := by
      intro hy
      rw [WellFounded.succ, dif_pos] at h'
      exact wo.wf.not_lt_min _ h hy h'
    rcases trichotomous_of r x y with (hy | hy | hy)
    exfalso
    exact this hy
    -- ‚ä¢ r y x ‚à® y = x
    right
    -- ‚ä¢ y = x
    exact hy.symm
    -- ‚ä¢ r y x ‚à® y = x
    left
    -- ‚ä¢ r y x
    exact hy
    -- üéâ no goals
  rintro (hy | rfl); exact _root_.trans hy (wo.wf.lt_succ h); exact wo.wf.lt_succ h
  -- ‚ä¢ r y (WellFounded.succ (_ : WellFounded r) x)
                     -- ‚ä¢ r y (WellFounded.succ (_ : WellFounded r) y)
                                                              -- üéâ no goals
#align well_founded.lt_succ_iff WellFounded.lt_succ_iff

section LinearOrder

variable [LinearOrder Œ≤] (h : WellFounded ((¬∑ < ¬∑) : Œ≤ ‚Üí Œ≤ ‚Üí Prop)) [PartialOrder Œ≥]

theorem min_le {x : Œ≤} {s : Set Œ≤} (hx : x ‚àà s) (hne : s.Nonempty := ‚ü®x, hx‚ü©) : h.min s hne ‚â§ x :=
  not_lt.1 <| h.not_lt_min _ _ hx
#align well_founded.min_le WellFounded.min_le

private theorem eq_strictMono_iff_eq_range_aux {f g : Œ≤ ‚Üí Œ≥} (hf : StrictMono f)
    (hg : StrictMono g) (hfg : Set.range f = Set.range g) {b : Œ≤} (H : ‚àÄ a < b, f a = g a) :
    f b ‚â§ g b := by
  obtain ‚ü®c, hc‚ü© : g b ‚àà Set.range f := by
    rw [hfg]
    exact Set.mem_range_self b
  cases' lt_or_le c b with hcb hbc
  -- ‚ä¢ f b ‚â§ g b
  ¬∑ rw [H c hcb] at hc
    -- ‚ä¢ f b ‚â§ g b
    rw [hg.injective hc] at hcb
    -- ‚ä¢ f b ‚â§ g b
    exact hcb.false.elim
    -- üéâ no goals
  ¬∑ rw [‚Üê hc]
    -- ‚ä¢ f b ‚â§ f c
    exact hf.monotone hbc
    -- üéâ no goals

theorem eq_strictMono_iff_eq_range {f g : Œ≤ ‚Üí Œ≥} (hf : StrictMono f) (hg : StrictMono g) :
    Set.range f = Set.range g ‚Üî f = g :=
  ‚ü®fun hfg => by
    funext a
    -- ‚ä¢ f a = g a
    apply h.induction a
    -- ‚ä¢ ‚àÄ (x : Œ≤), (‚àÄ (y : Œ≤), y < x ‚Üí f y = g y) ‚Üí f x = g x
    exact fun b H =>
      le_antisymm (eq_strictMono_iff_eq_range_aux hf hg hfg H)
        (eq_strictMono_iff_eq_range_aux hg hf hfg.symm fun a hab => (H a hab).symm),
    congr_arg _‚ü©
#align well_founded.eq_strict_mono_iff_eq_range WellFounded.eq_strictMono_iff_eq_range

theorem self_le_of_strictMono {f : Œ≤ ‚Üí Œ≤} (hf : StrictMono f) : ‚àÄ n, n ‚â§ f n := by
  by_contra' h‚ÇÅ
  -- ‚ä¢ False
  have h‚ÇÇ := h.min_mem _ h‚ÇÅ
  -- ‚ä¢ False
  exact h.not_lt_min _ h‚ÇÅ (hf h‚ÇÇ) h‚ÇÇ
  -- üéâ no goals
#align well_founded.self_le_of_strict_mono WellFounded.self_le_of_strictMono

end LinearOrder

end WellFounded

namespace Function

variable (f : Œ± ‚Üí Œ≤)

section LT

variable [LT Œ≤] (h : WellFounded ((¬∑ < ¬∑) : Œ≤ ‚Üí Œ≤ ‚Üí Prop))

/-- Given a function `f : Œ± ‚Üí Œ≤` where `Œ≤` carries a well-founded `<`, this is an element of `Œ±`
whose image under `f` is minimal in the sense of `Function.not_lt_argmin`. -/
noncomputable def argmin [Nonempty Œ±] : Œ± :=
  WellFounded.min (InvImage.wf f h) Set.univ Set.univ_nonempty
#align function.argmin Function.argmin

theorem not_lt_argmin [Nonempty Œ±] (a : Œ±) : ¬¨f a < f (argmin f h) :=
  WellFounded.not_lt_min (InvImage.wf f h) _ _ (Set.mem_univ a)
#align function.not_lt_argmin Function.not_lt_argmin

/-- Given a function `f : Œ± ‚Üí Œ≤` where `Œ≤` carries a well-founded `<`, and a non-empty subset `s`
of `Œ±`, this is an element of `s` whose image under `f` is minimal in the sense of
`Function.not_lt_argminOn`. -/
noncomputable def argminOn (s : Set Œ±) (hs : s.Nonempty) : Œ± :=
  WellFounded.min (InvImage.wf f h) s hs
#align function.argmin_on Function.argminOn

@[simp]
theorem argminOn_mem (s : Set Œ±) (hs : s.Nonempty) : argminOn f h s hs ‚àà s :=
  WellFounded.min_mem _ _ _
#align function.argmin_on_mem Function.argminOn_mem

--Porting note: @[simp] removed as it will never apply
theorem not_lt_argminOn (s : Set Œ±) {a : Œ±} (ha : a ‚àà s)
    (hs : s.Nonempty := Set.nonempty_of_mem ha) : ¬¨f a < f (argminOn f h s hs) :=
  WellFounded.not_lt_min (InvImage.wf f h) s hs ha
#align function.not_lt_argmin_on Function.not_lt_argminOn

end LT

section LinearOrder

variable [LinearOrder Œ≤] (h : WellFounded ((¬∑ < ¬∑) : Œ≤ ‚Üí Œ≤ ‚Üí Prop))

--Porting note: @[simp] removed as it will never apply
theorem argmin_le (a : Œ±) [Nonempty Œ±] : f (argmin f h) ‚â§ f a :=
  not_lt.mp <| not_lt_argmin f h a
#align function.argmin_le Function.argmin_le

--Porting note: @[simp] removed as it will never apply
theorem argminOn_le (s : Set Œ±) {a : Œ±} (ha : a ‚àà s) (hs : s.Nonempty := Set.nonempty_of_mem ha) :
    f (argminOn f h s hs) ‚â§ f a :=
  not_lt.mp <| not_lt_argminOn f h s ha hs
#align function.argmin_on_le Function.argminOn_le

end LinearOrder

end Function

section Induction

/-- Let `r` be a relation on `Œ±`, let `f : Œ± ‚Üí Œ≤` be a function, let `C : Œ≤ ‚Üí Prop`, and
let `bot : Œ±`. This induction principle shows that `C (f bot)` holds, given that
* some `a` that is accessible by `r` satisfies `C (f a)`, and
* for each `b` such that `f b ‚â† f bot` and `C (f b)` holds, there is `c`
  satisfying `r c b` and `C (f c)`. -/
theorem Acc.induction_bot' {Œ± Œ≤} {r : Œ± ‚Üí Œ± ‚Üí Prop} {a bot : Œ±} (ha : Acc r a) {C : Œ≤ ‚Üí Prop}
    {f : Œ± ‚Üí Œ≤} (ih : ‚àÄ b, f b ‚â† f bot ‚Üí C (f b) ‚Üí ‚àÉ c, r c b ‚àß C (f c)) : C (f a) ‚Üí C (f bot) :=
  (@Acc.recOn _ _ (fun x _ => C (f x) ‚Üí C (f bot)) _ ha) fun x _ ih' hC =>
    (eq_or_ne (f x) (f bot)).elim (fun h => h ‚ñ∏ hC) (fun h =>
      let ‚ü®y, hy‚ÇÅ, hy‚ÇÇ‚ü© := ih x h hC
      ih' y hy‚ÇÅ hy‚ÇÇ)
#align acc.induction_bot' Acc.induction_bot'

/-- Let `r` be a relation on `Œ±`, let `C : Œ± ‚Üí Prop` and let `bot : Œ±`.
This induction principle shows that `C bot` holds, given that
* some `a` that is accessible by `r` satisfies `C a`, and
* for each `b ‚â† bot` such that `C b` holds, there is `c` satisfying `r c b` and `C c`. -/
theorem Acc.induction_bot {Œ±} {r : Œ± ‚Üí Œ± ‚Üí Prop} {a bot : Œ±} (ha : Acc r a) {C : Œ± ‚Üí Prop}
    (ih : ‚àÄ b, b ‚â† bot ‚Üí C b ‚Üí ‚àÉ c, r c b ‚àß C c) : C a ‚Üí C bot :=
  ha.induction_bot' ih
#align acc.induction_bot Acc.induction_bot

/-- Let `r` be a well-founded relation on `Œ±`, let `f : Œ± ‚Üí Œ≤` be a function,
let `C : Œ≤ ‚Üí Prop`, and let `bot : Œ±`.
This induction principle shows that `C (f bot)` holds, given that
* some `a` satisfies `C (f a)`, and
* for each `b` such that `f b ‚â† f bot` and `C (f b)` holds, there is `c`
  satisfying `r c b` and `C (f c)`. -/
theorem WellFounded.induction_bot' {Œ± Œ≤} {r : Œ± ‚Üí Œ± ‚Üí Prop} (hwf : WellFounded r) {a bot : Œ±}
    {C : Œ≤ ‚Üí Prop} {f : Œ± ‚Üí Œ≤} (ih : ‚àÄ b, f b ‚â† f bot ‚Üí C (f b) ‚Üí ‚àÉ c, r c b ‚àß C (f c)) :
    C (f a) ‚Üí C (f bot) :=
  (hwf.apply a).induction_bot' ih
#align well_founded.induction_bot' WellFounded.induction_bot'

/-- Let `r` be a well-founded relation on `Œ±`, let `C : Œ± ‚Üí Prop`, and let `bot : Œ±`.
This induction principle shows that `C bot` holds, given that
* some `a` satisfies `C a`, and
* for each `b` that satisfies `C b`, there is `c` satisfying `r c b` and `C c`.

The naming is inspired by the fact that when `r` is transitive, it follows that `bot` is
the smallest element w.r.t. `r` that satisfies `C`. -/
theorem WellFounded.induction_bot {Œ±} {r : Œ± ‚Üí Œ± ‚Üí Prop} (hwf : WellFounded r) {a bot : Œ±}
    {C : Œ± ‚Üí Prop} (ih : ‚àÄ b, b ‚â† bot ‚Üí C b ‚Üí ‚àÉ c, r c b ‚àß C c) : C a ‚Üí C bot :=
  hwf.induction_bot' ih
#align well_founded.induction_bot WellFounded.induction_bot

end Induction
