/-
Copyright (c) 2014 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Yury Kudryashov, YaÃ«l Dillies
-/
import Mathlib.Order.Synonym

#align_import order.max from "leanprover-community/mathlib"@"6623e6af705e97002a9054c1c05a980180276fc1"

/-!
# Minimal/maximal and bottom/top elements

This file defines predicates for elements to be minimal/maximal or bottom/top and typeclasses
saying that there are no such elements.

## Predicates

* `IsBot`: An element is *bottom* if all elements are greater than it.
* `IsTop`: An element is *top* if all elements are less than it.
* `IsMin`: An element is *minimal* if no element is strictly less than it.
* `IsMax`: An element is *maximal* if no element is strictly greater than it.

See also `isBot_iff_isMin` and `isTop_iff_isMax` for the equivalences in a (co)directed order.

## Typeclasses

* `NoBotOrder`: An order without bottom elements.
* `NoTopOrder`: An order without top elements.
* `NoMinOrder`: An order without minimal elements.
* `NoMaxOrder`: An order without maximal elements.
-/


open OrderDual

universe u v

variable {Î± Î² : Type*}

/-- Order without bottom elements. -/
class NoBotOrder (Î± : Type*) [LE Î±] : Prop where
  /-- For each term `a`, there is some `b` which is either incomparable or strictly smaller. -/
  exists_not_ge (a : Î±) : âˆƒ b, Â¬a â‰¤ b
#align no_bot_order NoBotOrder

/-- Order without top elements. -/
class NoTopOrder (Î± : Type*) [LE Î±] : Prop where
  /-- For each term `a`, there is some `b` which is either incomparable or strictly larger. -/
  exists_not_le (a : Î±) : âˆƒ b, Â¬b â‰¤ a
#align no_top_order NoTopOrder

/-- Order without minimal elements. Sometimes called coinitial or dense. -/
class NoMinOrder (Î± : Type*) [LT Î±] : Prop where
  /-- For each term `a`, there is some strictly smaller `b`. -/
  exists_lt (a : Î±) : âˆƒ b, b < a
#align no_min_order NoMinOrder

/-- Order without maximal elements. Sometimes called cofinal. -/
class NoMaxOrder (Î± : Type*) [LT Î±] : Prop where
  /-- For each term `a`, there is some strictly greater `b`. -/
  exists_gt (a : Î±) : âˆƒ b, a < b
#align no_max_order NoMaxOrder

export NoBotOrder (exists_not_ge)

export NoTopOrder (exists_not_le)

export NoMinOrder (exists_lt)

export NoMaxOrder (exists_gt)

instance nonempty_lt [LT Î±] [NoMinOrder Î±] (a : Î±) : Nonempty { x // x < a } :=
  nonempty_subtype.2 (exists_lt a)

instance nonempty_gt [LT Î±] [NoMaxOrder Î±] (a : Î±) : Nonempty { x // a < x } :=
  nonempty_subtype.2 (exists_gt a)

instance OrderDual.noBotOrder [LE Î±] [NoTopOrder Î±] : NoBotOrder Î±áµ’áµˆ :=
  âŸ¨fun a => @exists_not_le Î± _ _ aâŸ©
#align order_dual.no_bot_order OrderDual.noBotOrder

instance OrderDual.noTopOrder [LE Î±] [NoBotOrder Î±] : NoTopOrder Î±áµ’áµˆ :=
  âŸ¨fun a => @exists_not_ge Î± _ _ aâŸ©
#align order_dual.no_top_order OrderDual.noTopOrder

instance OrderDual.noMinOrder [LT Î±] [NoMaxOrder Î±] : NoMinOrder Î±áµ’áµˆ :=
  âŸ¨fun a => @exists_gt Î± _ _ aâŸ©
#align order_dual.no_min_order OrderDual.noMinOrder

instance OrderDual.noMaxOrder [LT Î±] [NoMinOrder Î±] : NoMaxOrder Î±áµ’áµˆ :=
  âŸ¨fun a => @exists_lt Î± _ _ aâŸ©
#align order_dual.no_max_order OrderDual.noMaxOrder

-- See note [lower instance priority]
instance (priority := 100) [Preorder Î±] [NoMinOrder Î±] : NoBotOrder Î± :=
  âŸ¨fun a => (exists_lt a).imp fun _ => not_le_of_ltâŸ©

-- See note [lower instance priority]
instance (priority := 100) [Preorder Î±] [NoMaxOrder Î±] : NoTopOrder Î± :=
  âŸ¨fun a => (exists_gt a).imp fun _ => not_le_of_ltâŸ©

instance noMaxOrder_of_left [Preorder Î±] [Preorder Î²] [NoMaxOrder Î±] : NoMaxOrder (Î± Ã— Î²) :=
  âŸ¨fun âŸ¨a, bâŸ© => by
    obtain âŸ¨c, hâŸ© := exists_gt a
    -- âŠ¢ âˆƒ b_1, (a, b) < b_1
    exact âŸ¨(c, b), Prod.mk_lt_mk_iff_left.2 hâŸ©âŸ©
    -- ðŸŽ‰ no goals
#align no_max_order_of_left noMaxOrder_of_left

instance noMaxOrder_of_right [Preorder Î±] [Preorder Î²] [NoMaxOrder Î²] : NoMaxOrder (Î± Ã— Î²) :=
  âŸ¨fun âŸ¨a, bâŸ© => by
    obtain âŸ¨c, hâŸ© := exists_gt b
    -- âŠ¢ âˆƒ b_1, (a, b) < b_1
    exact âŸ¨(a, c), Prod.mk_lt_mk_iff_right.2 hâŸ©âŸ©
    -- ðŸŽ‰ no goals
#align no_max_order_of_right noMaxOrder_of_right

instance noMinOrder_of_left [Preorder Î±] [Preorder Î²] [NoMinOrder Î±] : NoMinOrder (Î± Ã— Î²) :=
  âŸ¨fun âŸ¨a, bâŸ© => by
    obtain âŸ¨c, hâŸ© := exists_lt a
    -- âŠ¢ âˆƒ b_1, b_1 < (a, b)
    exact âŸ¨(c, b), Prod.mk_lt_mk_iff_left.2 hâŸ©âŸ©
    -- ðŸŽ‰ no goals
#align no_min_order_of_left noMinOrder_of_left

instance noMinOrder_of_right [Preorder Î±] [Preorder Î²] [NoMinOrder Î²] : NoMinOrder (Î± Ã— Î²) :=
  âŸ¨fun âŸ¨a, bâŸ© => by
    obtain âŸ¨c, hâŸ© := exists_lt b
    -- âŠ¢ âˆƒ b_1, b_1 < (a, b)
    exact âŸ¨(a, c), Prod.mk_lt_mk_iff_right.2 hâŸ©âŸ©
    -- ðŸŽ‰ no goals
#align no_min_order_of_right noMinOrder_of_right

instance {Î¹ : Type u} {Ï€ : Î¹ â†’ Type*} [Nonempty Î¹] [âˆ€ i, Preorder (Ï€ i)] [âˆ€ i, NoMaxOrder (Ï€ i)] :
    NoMaxOrder (âˆ€ i, Ï€ i) :=
  âŸ¨fun a => by
    classical
    obtain âŸ¨b, hbâŸ© := exists_gt (a <| Classical.arbitrary _)
    exact âŸ¨_, lt_update_self_iff.2 hbâŸ©âŸ©

instance {Î¹ : Type u} {Ï€ : Î¹ â†’ Type*} [Nonempty Î¹] [âˆ€ i, Preorder (Ï€ i)] [âˆ€ i, NoMinOrder (Ï€ i)] :
    NoMinOrder (âˆ€ i, Ï€ i) :=
  âŸ¨fun a => by
     classical
      obtain âŸ¨b, hbâŸ© := exists_lt (a <| Classical.arbitrary _)
      exact âŸ¨_, update_lt_self_iff.2 hbâŸ©âŸ©

-- Porting note: mathlib3 proof uses `convert`
theorem NoBotOrder.to_noMinOrder (Î± : Type*) [LinearOrder Î±] [NoBotOrder Î±] : NoMinOrder Î± :=
  { exists_lt := fun a => by simpa [not_le] using exists_not_ge a }
                             -- ðŸŽ‰ no goals
#align no_bot_order.to_no_min_order NoBotOrder.to_noMinOrder

-- Porting note: mathlib3 proof uses `convert`
theorem NoTopOrder.to_noMaxOrder (Î± : Type*) [LinearOrder Î±] [NoTopOrder Î±] : NoMaxOrder Î± :=
  { exists_gt := fun a => by simpa [not_le] using exists_not_le a }
                             -- ðŸŽ‰ no goals
#align no_top_order.to_no_max_order NoTopOrder.to_noMaxOrder

theorem noBotOrder_iff_noMinOrder (Î± : Type*) [LinearOrder Î±] : NoBotOrder Î± â†” NoMinOrder Î± :=
  âŸ¨fun h =>
    haveI := h
    NoBotOrder.to_noMinOrder Î±,
    fun h =>
    haveI := h
    inferInstanceâŸ©
#align no_bot_order_iff_no_min_order noBotOrder_iff_noMinOrder

theorem noTopOrder_iff_noMaxOrder (Î± : Type*) [LinearOrder Î±] : NoTopOrder Î± â†” NoMaxOrder Î± :=
  âŸ¨fun h =>
    haveI := h
    NoTopOrder.to_noMaxOrder Î±,
    fun h =>
    haveI := h
    inferInstanceâŸ©
#align no_top_order_iff_no_max_order noTopOrder_iff_noMaxOrder

theorem NoMinOrder.not_acc [LT Î±] [NoMinOrder Î±] (a : Î±) : Â¬Acc (Â· < Â·) a := fun h =>
  Acc.recOn h fun x _ => (exists_lt x).recOn
#align no_min_order.not_acc NoMinOrder.not_acc

theorem NoMaxOrder.not_acc [LT Î±] [NoMaxOrder Î±] (a : Î±) : Â¬Acc (Â· > Â·) a := fun h =>
  Acc.recOn h fun x _ => (exists_gt x).recOn
#align no_max_order.not_acc NoMaxOrder.not_acc

section LE

variable [LE Î±] {a b : Î±}

/-- `a : Î±` is a bottom element of `Î±` if it is less than or equal to any other element of `Î±`.
This predicate is roughly an unbundled version of `OrderBot`, except that a preorder may have
several bottom elements. When `Î±` is linear, this is useful to make a case disjunction on
`NoMinOrder Î±` within a proof. -/
def IsBot (a : Î±) : Prop :=
  âˆ€ b, a â‰¤ b
#align is_bot IsBot

/-- `a : Î±` is a top element of `Î±` if it is greater than or equal to any other element of `Î±`.
This predicate is roughly an unbundled version of `OrderBot`, except that a preorder may have
several top elements. When `Î±` is linear, this is useful to make a case disjunction on
`NoMaxOrder Î±` within a proof. -/
def IsTop (a : Î±) : Prop :=
  âˆ€ b, b â‰¤ a
#align is_top IsTop

/-- `a` is a minimal element of `Î±` if no element is strictly less than it. We spell it without `<`
to avoid having to convert between `â‰¤` and `<`. Instead, `isMin_iff_forall_not_lt` does the
conversion. -/
def IsMin (a : Î±) : Prop :=
  âˆ€ â¦ƒbâ¦„, b â‰¤ a â†’ a â‰¤ b
#align is_min IsMin

/-- `a` is a maximal element of `Î±` if no element is strictly greater than it. We spell it without
`<` to avoid having to convert between `â‰¤` and `<`. Instead, `isMax_iff_forall_not_lt` does the
conversion. -/
def IsMax (a : Î±) : Prop :=
  âˆ€ â¦ƒbâ¦„, a â‰¤ b â†’ b â‰¤ a
#align is_max IsMax

@[simp]
theorem not_isBot [NoBotOrder Î±] (a : Î±) : Â¬IsBot a := fun h =>
  let âŸ¨_, hbâŸ© := exists_not_ge a
  hb <| h _
#align not_is_bot not_isBot

@[simp]
theorem not_isTop [NoTopOrder Î±] (a : Î±) : Â¬IsTop a := fun h =>
  let âŸ¨_, hbâŸ© := exists_not_le a
  hb <| h _
#align not_is_top not_isTop

protected theorem IsBot.isMin (h : IsBot a) : IsMin a := fun b _ => h b
#align is_bot.is_min IsBot.isMin

protected theorem IsTop.isMax (h : IsTop a) : IsMax a := fun b _ => h b
#align is_top.is_max IsTop.isMax

@[simp]
theorem isBot_toDual_iff : IsBot (toDual a) â†” IsTop a :=
  Iff.rfl
#align is_bot_to_dual_iff isBot_toDual_iff

@[simp]
theorem isTop_toDual_iff : IsTop (toDual a) â†” IsBot a :=
  Iff.rfl
#align is_top_to_dual_iff isTop_toDual_iff

@[simp]
theorem isMin_toDual_iff : IsMin (toDual a) â†” IsMax a :=
  Iff.rfl
#align is_min_to_dual_iff isMin_toDual_iff

@[simp]
theorem isMax_toDual_iff : IsMax (toDual a) â†” IsMin a :=
  Iff.rfl
#align is_max_to_dual_iff isMax_toDual_iff

@[simp]
theorem isBot_ofDual_iff {a : Î±áµ’áµˆ} : IsBot (ofDual a) â†” IsTop a :=
  Iff.rfl
#align is_bot_of_dual_iff isBot_ofDual_iff

@[simp]
theorem isTop_ofDual_iff {a : Î±áµ’áµˆ} : IsTop (ofDual a) â†” IsBot a :=
  Iff.rfl
#align is_top_of_dual_iff isTop_ofDual_iff

@[simp]
theorem isMin_ofDual_iff {a : Î±áµ’áµˆ} : IsMin (ofDual a) â†” IsMax a :=
  Iff.rfl
#align is_min_of_dual_iff isMin_ofDual_iff

@[simp]
theorem isMax_ofDual_iff {a : Î±áµ’áµˆ} : IsMax (ofDual a) â†” IsMin a :=
  Iff.rfl
#align is_max_of_dual_iff isMax_ofDual_iff

alias âŸ¨_, IsTop.toDualâŸ© := isBot_toDual_iff
#align is_top.to_dual IsTop.toDual

alias âŸ¨_, IsBot.toDualâŸ© := isTop_toDual_iff
#align is_bot.to_dual IsBot.toDual

alias âŸ¨_, IsMax.toDualâŸ© := isMin_toDual_iff
#align is_max.to_dual IsMax.toDual

alias âŸ¨_, IsMin.toDualâŸ© := isMax_toDual_iff
#align is_min.to_dual IsMin.toDual

alias âŸ¨_, IsTop.ofDualâŸ© := isBot_ofDual_iff
#align is_top.of_dual IsTop.ofDual

alias âŸ¨_, IsBot.ofDualâŸ© := isTop_ofDual_iff
#align is_bot.of_dual IsBot.ofDual

alias âŸ¨_, IsMax.ofDualâŸ© := isMin_ofDual_iff
#align is_max.of_dual IsMax.ofDual

alias âŸ¨_, IsMin.ofDualâŸ© := isMax_ofDual_iff
#align is_min.of_dual IsMin.ofDual

end LE

section Preorder

variable [Preorder Î±] {a b : Î±}

theorem IsBot.mono (ha : IsBot a) (h : b â‰¤ a) : IsBot b := fun _ => h.trans <| ha _
#align is_bot.mono IsBot.mono

theorem IsTop.mono (ha : IsTop a) (h : a â‰¤ b) : IsTop b := fun _ => (ha _).trans h
#align is_top.mono IsTop.mono

theorem IsMin.mono (ha : IsMin a) (h : b â‰¤ a) : IsMin b := fun _ hc => h.trans <| ha <| hc.trans h
#align is_min.mono IsMin.mono

theorem IsMax.mono (ha : IsMax a) (h : a â‰¤ b) : IsMax b := fun _ hc => (ha <| h.trans hc).trans h
#align is_max.mono IsMax.mono

theorem IsMin.not_lt (h : IsMin a) : Â¬b < a := fun hb => hb.not_le <| h hb.le
#align is_min.not_lt IsMin.not_lt

theorem IsMax.not_lt (h : IsMax a) : Â¬a < b := fun hb => hb.not_le <| h hb.le
#align is_max.not_lt IsMax.not_lt

@[simp]
theorem not_isMin_of_lt (h : b < a) : Â¬IsMin a := fun ha => ha.not_lt h
#align not_is_min_of_lt not_isMin_of_lt

@[simp]
theorem not_isMax_of_lt (h : a < b) : Â¬IsMax a := fun ha => ha.not_lt h
#align not_is_max_of_lt not_isMax_of_lt

alias LT.lt.not_isMin := not_isMin_of_lt

alias LT.lt.not_isMax := not_isMax_of_lt

theorem isMin_iff_forall_not_lt : IsMin a â†” âˆ€ b, Â¬b < a :=
  âŸ¨fun h _ => h.not_lt, fun h _ hba => of_not_not fun hab => h _ <| hba.lt_of_not_le habâŸ©
#align is_min_iff_forall_not_lt isMin_iff_forall_not_lt

theorem isMax_iff_forall_not_lt : IsMax a â†” âˆ€ b, Â¬a < b :=
  âŸ¨fun h _ => h.not_lt, fun h _ hba => of_not_not fun hab => h _ <| hba.lt_of_not_le habâŸ©
#align is_max_iff_forall_not_lt isMax_iff_forall_not_lt

@[simp]
theorem not_isMin_iff : Â¬IsMin a â†” âˆƒ b, b < a := by
  simp [lt_iff_le_not_le, IsMin, not_forall, exists_prop]
  -- ðŸŽ‰ no goals
#align not_is_min_iff not_isMin_iff

@[simp]
theorem not_isMax_iff : Â¬IsMax a â†” âˆƒ b, a < b := by
  simp [lt_iff_le_not_le, IsMax, not_forall, exists_prop]
  -- ðŸŽ‰ no goals
#align not_is_max_iff not_isMax_iff

@[simp]
theorem not_isMin [NoMinOrder Î±] (a : Î±) : Â¬IsMin a :=
  not_isMin_iff.2 <| exists_lt a
#align not_is_min not_isMin

@[simp]
theorem not_isMax [NoMaxOrder Î±] (a : Î±) : Â¬IsMax a :=
  not_isMax_iff.2 <| exists_gt a
#align not_is_max not_isMax

namespace Subsingleton

variable [Subsingleton Î±]

protected theorem isBot (a : Î±) : IsBot a := fun _ => (Subsingleton.elim _ _).le
#align subsingleton.is_bot Subsingleton.isBot

protected theorem isTop (a : Î±) : IsTop a := fun _ => (Subsingleton.elim _ _).le
#align subsingleton.is_top Subsingleton.isTop

protected theorem isMin (a : Î±) : IsMin a :=
  (Subsingleton.isBot _).isMin
#align subsingleton.is_min Subsingleton.isMin

protected theorem isMax (a : Î±) : IsMax a :=
  (Subsingleton.isTop _).isMax
#align subsingleton.is_max Subsingleton.isMax

end Subsingleton

end Preorder

section PartialOrder

variable [PartialOrder Î±] {a b : Î±}

protected theorem IsMin.eq_of_le (ha : IsMin a) (h : b â‰¤ a) : b = a :=
  h.antisymm <| ha h
#align is_min.eq_of_le IsMin.eq_of_le

protected theorem IsMin.eq_of_ge (ha : IsMin a) (h : b â‰¤ a) : a = b :=
  h.antisymm' <| ha h
#align is_min.eq_of_ge IsMin.eq_of_ge

protected theorem IsMax.eq_of_le (ha : IsMax a) (h : a â‰¤ b) : a = b :=
  h.antisymm <| ha h
#align is_max.eq_of_le IsMax.eq_of_le

protected theorem IsMax.eq_of_ge (ha : IsMax a) (h : a â‰¤ b) : b = a :=
  h.antisymm' <| ha h
#align is_max.eq_of_ge IsMax.eq_of_ge

end PartialOrder

section Prod

variable [Preorder Î±] [Preorder Î²] {a aâ‚ aâ‚‚ : Î±} {b bâ‚ bâ‚‚ : Î²} {x y : Î± Ã— Î²}

theorem IsBot.prod_mk (ha : IsBot a) (hb : IsBot b) : IsBot (a, b) := fun _ => âŸ¨ha _, hb _âŸ©
#align is_bot.prod_mk IsBot.prod_mk

theorem IsTop.prod_mk (ha : IsTop a) (hb : IsTop b) : IsTop (a, b) := fun _ => âŸ¨ha _, hb _âŸ©
#align is_top.prod_mk IsTop.prod_mk

theorem IsMin.prod_mk (ha : IsMin a) (hb : IsMin b) : IsMin (a, b) := fun _ hc => âŸ¨ha hc.1, hb hc.2âŸ©
#align is_min.prod_mk IsMin.prod_mk

theorem IsMax.prod_mk (ha : IsMax a) (hb : IsMax b) : IsMax (a, b) := fun _ hc => âŸ¨ha hc.1, hb hc.2âŸ©
#align is_max.prod_mk IsMax.prod_mk

theorem IsBot.fst (hx : IsBot x) : IsBot x.1 := fun c => (hx (c, x.2)).1
#align is_bot.fst IsBot.fst

theorem IsBot.snd (hx : IsBot x) : IsBot x.2 := fun c => (hx (x.1, c)).2
#align is_bot.snd IsBot.snd

theorem IsTop.fst (hx : IsTop x) : IsTop x.1 := fun c => (hx (c, x.2)).1
#align is_top.fst IsTop.fst

theorem IsTop.snd (hx : IsTop x) : IsTop x.2 := fun c => (hx (x.1, c)).2
#align is_top.snd IsTop.snd

theorem IsMin.fst (hx : IsMin x) : IsMin x.1 :=
  fun c hc => (hx <| show (c, x.2) â‰¤ x from (and_iff_left le_rfl).2 hc).1
#align is_min.fst IsMin.fst

theorem IsMin.snd (hx : IsMin x) : IsMin x.2 :=
  fun c hc => (hx <| show (x.1, c) â‰¤ x from (and_iff_right le_rfl).2 hc).2
#align is_min.snd IsMin.snd

theorem IsMax.fst (hx : IsMax x) : IsMax x.1 :=
  fun c hc => (hx <| show x â‰¤ (c, x.2) from (and_iff_left le_rfl).2 hc).1
#align is_max.fst IsMax.fst

theorem IsMax.snd (hx : IsMax x) : IsMax x.2 :=
  fun c hc => (hx <| show x â‰¤ (x.1, c) from (and_iff_right le_rfl).2 hc).2
#align is_max.snd IsMax.snd

theorem Prod.isBot_iff : IsBot x â†” IsBot x.1 âˆ§ IsBot x.2 :=
  âŸ¨fun hx => âŸ¨hx.fst, hx.sndâŸ©, fun h => h.1.prod_mk h.2âŸ©
#align prod.is_bot_iff Prod.isBot_iff

theorem Prod.isTop_iff : IsTop x â†” IsTop x.1 âˆ§ IsTop x.2 :=
  âŸ¨fun hx => âŸ¨hx.fst, hx.sndâŸ©, fun h => h.1.prod_mk h.2âŸ©
#align prod.is_top_iff Prod.isTop_iff

theorem Prod.isMin_iff : IsMin x â†” IsMin x.1 âˆ§ IsMin x.2 :=
  âŸ¨fun hx => âŸ¨hx.fst, hx.sndâŸ©, fun h => h.1.prod_mk h.2âŸ©
#align prod.is_min_iff Prod.isMin_iff

theorem Prod.isMax_iff : IsMax x â†” IsMax x.1 âˆ§ IsMax x.2 :=
  âŸ¨fun hx => âŸ¨hx.fst, hx.sndâŸ©, fun h => h.1.prod_mk h.2âŸ©
#align prod.is_max_iff Prod.isMax_iff

end Prod
