/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl
-/
import Mathlib.Data.Option.Basic
import Mathlib.Order.Lattice
import Mathlib.Order.ULift

#align_import order.bounded_order from "leanprover-community/mathlib"@"70d50ecfd4900dd6d328da39ab7ebd516abe4025"

/-!
# ‚ä§ and ‚ä•, bounded lattices and variants

This file defines top and bottom elements (greatest and least elements) of a type, the bounded
variants of different kinds of lattices, sets up the typeclass hierarchy between them and provides
instances for `Prop` and `fun`.

## Main declarations

* `<Top/Bot> Œ±`: Typeclasses to declare the `‚ä§`/`‚ä•` notation.
* `Order<Top/Bot> Œ±`: Order with a top/bottom element.
* `BoundedOrder Œ±`: Order with a top and bottom element.

## Common lattices

* Distributive lattices with a bottom element. Notated by `[DistribLattice Œ±] [OrderBot Œ±]`
  It captures the properties of `Disjoint` that are common to `GeneralizedBooleanAlgebra` and
  `DistribLattice` when `OrderBot`.
* Bounded and distributive lattice. Notated by `[DistribLattice Œ±] [BoundedOrder Œ±]`.
  Typical examples include `Prop` and `Det Œ±`.
-/

open Function OrderDual

universe u v

variable {Œ± : Type u} {Œ≤ : Type v} {Œ≥ Œ¥ : Type*}

/-! ### Top, bottom element -/


/-- Typeclass for the `‚ä§` (`\top`) notation -/
@[notation_class, ext]
class Top (Œ± : Type u) where
  /-- The top (`‚ä§`, `\top`) element -/
  top : Œ±
#align has_top Top

/-- Typeclass for the `‚ä•` (`\bot`) notation -/
@[notation_class, ext]
class Bot (Œ± : Type u) where
  /-- The bot (`‚ä•`, `\bot`) element -/
  bot : Œ±
#align has_bot Bot

/-- The top (`‚ä§`, `\top`) element -/
notation "‚ä§" => Top.top

/-- The bot (`‚ä•`, `\bot`) element -/
notation "‚ä•" => Bot.bot

instance (priority := 100) top_nonempty (Œ± : Type u) [Top Œ±] : Nonempty Œ± :=
  ‚ü®‚ä§‚ü©
#align has_top_nonempty top_nonempty

instance (priority := 100) bot_nonempty (Œ± : Type u) [Bot Œ±] : Nonempty Œ± :=
  ‚ü®‚ä•‚ü©
#align has_bot_nonempty bot_nonempty

attribute [match_pattern] Bot.bot Top.top

/-- An order is an `OrderTop` if it has a greatest element.
We state this using a data mixin, holding the value of `‚ä§` and the greatest element constraint. -/
class OrderTop (Œ± : Type u) [LE Œ±] extends Top Œ± where
  /-- `‚ä§` is the greatest element -/
  le_top : ‚àÄ a : Œ±, a ‚â§ ‚ä§
#align order_top OrderTop

section OrderTop

/-- An order is (noncomputably) either an `OrderTop` or a `NoTopOrder`. Use as
`casesI topOrderOrNoTopOrder Œ±`. -/
noncomputable def topOrderOrNoTopOrder (Œ± : Type*) [LE Œ±] : PSum (OrderTop Œ±) (NoTopOrder Œ±) := by
  by_cases H : ‚àÄ a : Œ±, ‚àÉ b, ¬¨b ‚â§ a
  -- ‚ä¢ OrderTop Œ± ‚äï' NoTopOrder Œ±
  ¬∑ exact PSum.inr ‚ü®H‚ü©
    -- üéâ no goals
  ¬∑ push_neg at H
    -- ‚ä¢ OrderTop Œ± ‚äï' NoTopOrder Œ±
    letI : Top Œ± := ‚ü®Classical.choose H‚ü©
    -- ‚ä¢ OrderTop Œ± ‚äï' NoTopOrder Œ±
    exact PSum.inl ‚ü®Classical.choose_spec H‚ü©
    -- üéâ no goals
#align top_order_or_no_top_order topOrderOrNoTopOrder

section LE

variable [LE Œ±] [OrderTop Œ±] {a : Œ±}

@[simp]
theorem le_top : a ‚â§ ‚ä§ :=
  OrderTop.le_top a
#align le_top le_top

@[simp]
theorem isTop_top : IsTop (‚ä§ : Œ±) := fun _ => le_top
#align is_top_top isTop_top

end LE

section Preorder

variable [Preorder Œ±] [OrderTop Œ±] {a b : Œ±}

@[simp]
theorem isMax_top : IsMax (‚ä§ : Œ±) :=
  isTop_top.isMax
#align is_max_top isMax_top

@[simp]
theorem not_top_lt : ¬¨‚ä§ < a :=
  isMax_top.not_lt
#align not_top_lt not_top_lt

theorem ne_top_of_lt (h : a < b) : a ‚â† ‚ä§ :=
  (h.trans_le le_top).ne
#align ne_top_of_lt ne_top_of_lt

alias LT.lt.ne_top := ne_top_of_lt

end Preorder

variable [PartialOrder Œ±] [OrderTop Œ±] [Preorder Œ≤] {f : Œ± ‚Üí Œ≤} {a b : Œ±}

@[simp]
theorem isMax_iff_eq_top : IsMax a ‚Üî a = ‚ä§ :=
  ‚ü®fun h => h.eq_of_le le_top, fun h _ _ => h.symm ‚ñ∏ le_top‚ü©
#align is_max_iff_eq_top isMax_iff_eq_top

@[simp]
theorem isTop_iff_eq_top : IsTop a ‚Üî a = ‚ä§ :=
  ‚ü®fun h => h.isMax.eq_of_le le_top, fun h _ => h.symm ‚ñ∏ le_top‚ü©
#align is_top_iff_eq_top isTop_iff_eq_top

theorem not_isMax_iff_ne_top : ¬¨IsMax a ‚Üî a ‚â† ‚ä§ :=
  isMax_iff_eq_top.not
#align not_is_max_iff_ne_top not_isMax_iff_ne_top

theorem not_isTop_iff_ne_top : ¬¨IsTop a ‚Üî a ‚â† ‚ä§ :=
  isTop_iff_eq_top.not
#align not_is_top_iff_ne_top not_isTop_iff_ne_top

alias ‚ü®IsMax.eq_top, _‚ü© := isMax_iff_eq_top
#align is_max.eq_top IsMax.eq_top

alias ‚ü®IsTop.eq_top, _‚ü© := isTop_iff_eq_top
#align is_top.eq_top IsTop.eq_top

@[simp]
theorem top_le_iff : ‚ä§ ‚â§ a ‚Üî a = ‚ä§ :=
  le_top.le_iff_eq.trans eq_comm
#align top_le_iff top_le_iff

theorem top_unique (h : ‚ä§ ‚â§ a) : a = ‚ä§ :=
  le_top.antisymm h
#align top_unique top_unique

theorem eq_top_iff : a = ‚ä§ ‚Üî ‚ä§ ‚â§ a :=
  top_le_iff.symm
#align eq_top_iff eq_top_iff

theorem eq_top_mono (h : a ‚â§ b) (h‚ÇÇ : a = ‚ä§) : b = ‚ä§ :=
  top_unique <| h‚ÇÇ ‚ñ∏ h
#align eq_top_mono eq_top_mono

theorem lt_top_iff_ne_top : a < ‚ä§ ‚Üî a ‚â† ‚ä§ :=
  le_top.lt_iff_ne
#align lt_top_iff_ne_top lt_top_iff_ne_top

@[simp]
theorem not_lt_top_iff : ¬¨a < ‚ä§ ‚Üî a = ‚ä§ :=
  lt_top_iff_ne_top.not_left
#align not_lt_top_iff not_lt_top_iff

theorem eq_top_or_lt_top (a : Œ±) : a = ‚ä§ ‚à® a < ‚ä§ :=
  le_top.eq_or_lt
#align eq_top_or_lt_top eq_top_or_lt_top

theorem Ne.lt_top (h : a ‚â† ‚ä§) : a < ‚ä§ :=
  lt_top_iff_ne_top.mpr h
#align ne.lt_top Ne.lt_top

theorem Ne.lt_top' (h : ‚ä§ ‚â† a) : a < ‚ä§ :=
  h.symm.lt_top
#align ne.lt_top' Ne.lt_top'

theorem ne_top_of_le_ne_top (hb : b ‚â† ‚ä§) (hab : a ‚â§ b) : a ‚â† ‚ä§ :=
  (hab.trans_lt hb.lt_top).ne
#align ne_top_of_le_ne_top ne_top_of_le_ne_top

theorem StrictMono.apply_eq_top_iff (hf : StrictMono f) : f a = f ‚ä§ ‚Üî a = ‚ä§ :=
  ‚ü®fun h => not_lt_top_iff.1 fun ha => (hf ha).ne h, congr_arg _‚ü©
#align strict_mono.apply_eq_top_iff StrictMono.apply_eq_top_iff

theorem StrictAnti.apply_eq_top_iff (hf : StrictAnti f) : f a = f ‚ä§ ‚Üî a = ‚ä§ :=
  ‚ü®fun h => not_lt_top_iff.1 fun ha => (hf ha).ne' h, congr_arg _‚ü©
#align strict_anti.apply_eq_top_iff StrictAnti.apply_eq_top_iff

variable [Nontrivial Œ±]

theorem not_isMin_top : ¬¨IsMin (‚ä§ : Œ±) := fun h =>
  let ‚ü®_, ha‚ü© := exists_ne (‚ä§ : Œ±)
  ha <| top_le_iff.1 <| h le_top
#align not_is_min_top not_isMin_top

end OrderTop

theorem StrictMono.maximal_preimage_top [LinearOrder Œ±] [Preorder Œ≤] [OrderTop Œ≤] {f : Œ± ‚Üí Œ≤}
    (H : StrictMono f) {a} (h_top : f a = ‚ä§) (x : Œ±) : x ‚â§ a :=
  H.maximal_of_maximal_image
    (fun p => by
      rw [h_top]
      -- ‚ä¢ p ‚â§ ‚ä§
      exact le_top)
      -- üéâ no goals
    x
#align strict_mono.maximal_preimage_top StrictMono.maximal_preimage_top

theorem OrderTop.ext_top {Œ±} {hA : PartialOrder Œ±} (A : OrderTop Œ±) {hB : PartialOrder Œ±}
    (B : OrderTop Œ±) (H : ‚àÄ x y : Œ±, (haveI := hA; x ‚â§ y) ‚Üî x ‚â§ y) :
    (@Top.top Œ± (@OrderTop.toTop Œ± hA.toLE A)) = (@Top.top Œ± (@OrderTop.toTop Œ± hB.toLE B)) := by
  cases PartialOrder.ext H
  -- ‚ä¢ ‚ä§ = ‚ä§
  apply top_unique
  -- ‚ä¢ ‚ä§ ‚â§ ‚ä§
  exact @le_top _ _ A _
  -- üéâ no goals
#align order_top.ext_top OrderTop.ext_top

theorem OrderTop.ext {Œ±} [PartialOrder Œ±] {A B : OrderTop Œ±} : A = B := by
  rcases A with ‚ü®ha‚ü©
  -- ‚ä¢ mk ha = B
  rcases B with ‚ü®hb‚ü©
  -- ‚ä¢ mk ha = mk hb
  congr
  -- ‚ä¢ toTop‚úù¬π = toTop‚úù
  ext
  -- ‚ä¢ ‚ä§ = ‚ä§
  exact le_antisymm (hb _) (ha _)
  -- üéâ no goals
#align order_top.ext OrderTop.ext

/-- An order is an `OrderBot` if it has a least element.
We state this using a data mixin, holding the value of `‚ä•` and the least element constraint. -/
class OrderBot (Œ± : Type u) [LE Œ±] extends Bot Œ± where
  /-- `‚ä•` is the least element -/
  bot_le : ‚àÄ a : Œ±, ‚ä• ‚â§ a
#align order_bot OrderBot

section OrderBot

/-- An order is (noncomputably) either an `OrderBot` or a `NoBotOrder`. Use as
`casesI botOrderOrNoBotOrder Œ±`. -/
noncomputable def botOrderOrNoBotOrder (Œ± : Type*) [LE Œ±] : PSum (OrderBot Œ±) (NoBotOrder Œ±) := by
  by_cases H : ‚àÄ a : Œ±, ‚àÉ b, ¬¨a ‚â§ b
  -- ‚ä¢ OrderBot Œ± ‚äï' NoBotOrder Œ±
  ¬∑ exact PSum.inr ‚ü®H‚ü©
    -- üéâ no goals
  ¬∑ push_neg at H
    -- ‚ä¢ OrderBot Œ± ‚äï' NoBotOrder Œ±
    letI : Bot Œ± := ‚ü®Classical.choose H‚ü©
    -- ‚ä¢ OrderBot Œ± ‚äï' NoBotOrder Œ±
    exact PSum.inl ‚ü®Classical.choose_spec H‚ü©
    -- üéâ no goals
#align bot_order_or_no_bot_order botOrderOrNoBotOrder

section LE

variable [LE Œ±] [OrderBot Œ±] {a : Œ±}

@[simp]
theorem bot_le : ‚ä• ‚â§ a :=
  OrderBot.bot_le a
#align bot_le bot_le

@[simp]
theorem isBot_bot : IsBot (‚ä• : Œ±) := fun _ => bot_le
#align is_bot_bot isBot_bot

end LE

namespace OrderDual

variable (Œ±)

instance top [Bot Œ±] : Top Œ±·µí·µà :=
  ‚ü®(‚ä• : Œ±)‚ü©

instance bot [Top Œ±] : Bot Œ±·µí·µà :=
  ‚ü®(‚ä§ : Œ±)‚ü©

instance orderTop [LE Œ±] [OrderBot Œ±] : OrderTop Œ±·µí·µà where
  __ := inferInstanceAs (Top Œ±·µí·µà)
  le_top := @bot_le Œ± _ _

instance orderBot [LE Œ±] [OrderTop Œ±] : OrderBot Œ±·µí·µà where
  __ := inferInstanceAs (Bot Œ±·µí·µà)
  bot_le := @le_top Œ± _ _

@[simp]
theorem ofDual_bot [Top Œ±] : ofDual ‚ä• = (‚ä§ : Œ±) :=
  rfl
#align order_dual.of_dual_bot OrderDual.ofDual_bot

@[simp]
theorem ofDual_top [Bot Œ±] : ofDual ‚ä§ = (‚ä• : Œ±) :=
  rfl
#align order_dual.of_dual_top OrderDual.ofDual_top

@[simp]
theorem toDual_bot [Bot Œ±] : toDual (‚ä• : Œ±) = ‚ä§ :=
  rfl
#align order_dual.to_dual_bot OrderDual.toDual_bot

@[simp]
theorem toDual_top [Top Œ±] : toDual (‚ä§ : Œ±) = ‚ä• :=
  rfl
#align order_dual.to_dual_top OrderDual.toDual_top

end OrderDual

section Preorder

variable [Preorder Œ±] [OrderBot Œ±] {a b : Œ±}

@[simp]
theorem isMin_bot : IsMin (‚ä• : Œ±) :=
  isBot_bot.isMin
#align is_min_bot isMin_bot

@[simp]
theorem not_lt_bot : ¬¨a < ‚ä• :=
  isMin_bot.not_lt
#align not_lt_bot not_lt_bot

theorem ne_bot_of_gt (h : a < b) : b ‚â† ‚ä• :=
  (bot_le.trans_lt h).ne'
#align ne_bot_of_gt ne_bot_of_gt

alias LT.lt.ne_bot := ne_bot_of_gt

end Preorder

variable [PartialOrder Œ±] [OrderBot Œ±] [Preorder Œ≤] {f : Œ± ‚Üí Œ≤} {a b : Œ±}

@[simp]
theorem isMin_iff_eq_bot : IsMin a ‚Üî a = ‚ä• :=
  ‚ü®fun h => h.eq_of_ge bot_le, fun h _ _ => h.symm ‚ñ∏ bot_le‚ü©
#align is_min_iff_eq_bot isMin_iff_eq_bot

@[simp]
theorem isBot_iff_eq_bot : IsBot a ‚Üî a = ‚ä• :=
  ‚ü®fun h => h.isMin.eq_of_ge bot_le, fun h _ => h.symm ‚ñ∏ bot_le‚ü©
#align is_bot_iff_eq_bot isBot_iff_eq_bot

theorem not_isMin_iff_ne_bot : ¬¨IsMin a ‚Üî a ‚â† ‚ä• :=
  isMin_iff_eq_bot.not
#align not_is_min_iff_ne_bot not_isMin_iff_ne_bot

theorem not_isBot_iff_ne_bot : ¬¨IsBot a ‚Üî a ‚â† ‚ä• :=
  isBot_iff_eq_bot.not
#align not_is_bot_iff_ne_bot not_isBot_iff_ne_bot

alias ‚ü®IsMin.eq_bot, _‚ü© := isMin_iff_eq_bot
#align is_min.eq_bot IsMin.eq_bot

alias ‚ü®IsBot.eq_bot, _‚ü© := isBot_iff_eq_bot
#align is_bot.eq_bot IsBot.eq_bot

@[simp]
theorem le_bot_iff : a ‚â§ ‚ä• ‚Üî a = ‚ä• :=
  bot_le.le_iff_eq
#align le_bot_iff le_bot_iff

theorem bot_unique (h : a ‚â§ ‚ä•) : a = ‚ä• :=
  h.antisymm bot_le
#align bot_unique bot_unique

theorem eq_bot_iff : a = ‚ä• ‚Üî a ‚â§ ‚ä• :=
  le_bot_iff.symm
#align eq_bot_iff eq_bot_iff

theorem eq_bot_mono (h : a ‚â§ b) (h‚ÇÇ : b = ‚ä•) : a = ‚ä• :=
  bot_unique <| h‚ÇÇ ‚ñ∏ h
#align eq_bot_mono eq_bot_mono

theorem bot_lt_iff_ne_bot : ‚ä• < a ‚Üî a ‚â† ‚ä• :=
  bot_le.lt_iff_ne.trans ne_comm
#align bot_lt_iff_ne_bot bot_lt_iff_ne_bot

@[simp]
theorem not_bot_lt_iff : ¬¨‚ä• < a ‚Üî a = ‚ä• :=
  bot_lt_iff_ne_bot.not_left
#align not_bot_lt_iff not_bot_lt_iff

theorem eq_bot_or_bot_lt (a : Œ±) : a = ‚ä• ‚à® ‚ä• < a :=
  bot_le.eq_or_gt
#align eq_bot_or_bot_lt eq_bot_or_bot_lt

theorem eq_bot_of_minimal (h : ‚àÄ b, ¬¨b < a) : a = ‚ä• :=
  (eq_bot_or_bot_lt a).resolve_right (h ‚ä•)
#align eq_bot_of_minimal eq_bot_of_minimal

theorem Ne.bot_lt (h : a ‚â† ‚ä•) : ‚ä• < a :=
  bot_lt_iff_ne_bot.mpr h
#align ne.bot_lt Ne.bot_lt

theorem Ne.bot_lt' (h : ‚ä• ‚â† a) : ‚ä• < a :=
  h.symm.bot_lt
#align ne.bot_lt' Ne.bot_lt'

theorem ne_bot_of_le_ne_bot (hb : b ‚â† ‚ä•) (hab : b ‚â§ a) : a ‚â† ‚ä• :=
  (hb.bot_lt.trans_le hab).ne'
#align ne_bot_of_le_ne_bot ne_bot_of_le_ne_bot

theorem StrictMono.apply_eq_bot_iff (hf : StrictMono f) : f a = f ‚ä• ‚Üî a = ‚ä• :=
  hf.dual.apply_eq_top_iff
#align strict_mono.apply_eq_bot_iff StrictMono.apply_eq_bot_iff

theorem StrictAnti.apply_eq_bot_iff (hf : StrictAnti f) : f a = f ‚ä• ‚Üî a = ‚ä• :=
  hf.dual.apply_eq_top_iff
#align strict_anti.apply_eq_bot_iff StrictAnti.apply_eq_bot_iff

variable [Nontrivial Œ±]

theorem not_isMax_bot : ¬¨IsMax (‚ä• : Œ±) :=
  @not_isMin_top Œ±·µí·µà _ _ _
#align not_is_max_bot not_isMax_bot

end OrderBot

theorem StrictMono.minimal_preimage_bot [LinearOrder Œ±] [PartialOrder Œ≤] [OrderBot Œ≤] {f : Œ± ‚Üí Œ≤}
    (H : StrictMono f) {a} (h_bot : f a = ‚ä•) (x : Œ±) : a ‚â§ x :=
  H.minimal_of_minimal_image
    (fun p => by
      rw [h_bot]
      -- ‚ä¢ ‚ä• ‚â§ p
      exact bot_le)
      -- üéâ no goals
    x
#align strict_mono.minimal_preimage_bot StrictMono.minimal_preimage_bot

theorem OrderBot.ext_bot {Œ±} {hA : PartialOrder Œ±} (A : OrderBot Œ±) {hB : PartialOrder Œ±}
    (B : OrderBot Œ±) (H : ‚àÄ x y : Œ±, (haveI := hA; x ‚â§ y) ‚Üî x ‚â§ y) :
    (@Bot.bot Œ± (@OrderBot.toBot Œ± hA.toLE A)) = (@Bot.bot Œ± (@OrderBot.toBot Œ± hB.toLE B)) := by
  cases PartialOrder.ext H
  -- ‚ä¢ ‚ä• = ‚ä•
  apply bot_unique
  -- ‚ä¢ ‚ä• ‚â§ ‚ä•
  exact @bot_le _ _ A _
  -- üéâ no goals
#align order_bot.ext_bot OrderBot.ext_bot

theorem OrderBot.ext {Œ±} [PartialOrder Œ±] {A B : OrderBot Œ±} : A = B := by
  rcases A with ‚ü®ha‚ü©
  -- ‚ä¢ mk ha = B
  rcases B with ‚ü®hb‚ü©
  -- ‚ä¢ mk ha = mk hb
  congr
  -- ‚ä¢ toBot‚úù¬π = toBot‚úù
  ext
  -- ‚ä¢ ‚ä• = ‚ä•
  exact le_antisymm (ha _) (hb _)
  -- üéâ no goals
#align order_bot.ext OrderBot.ext

section SemilatticeSupTop

variable [SemilatticeSup Œ±] [OrderTop Œ±] {a : Œ±}

-- Porting note: Not simp because simp can prove it
theorem top_sup_eq : ‚ä§ ‚äî a = ‚ä§ :=
  sup_of_le_left le_top
#align top_sup_eq top_sup_eq

-- Porting note: Not simp because simp can prove it
theorem sup_top_eq : a ‚äî ‚ä§ = ‚ä§ :=
  sup_of_le_right le_top
#align sup_top_eq sup_top_eq

end SemilatticeSupTop

section SemilatticeSupBot

variable [SemilatticeSup Œ±] [OrderBot Œ±] {a b : Œ±}

-- Porting note: Not simp because simp can prove it
theorem bot_sup_eq : ‚ä• ‚äî a = a :=
  sup_of_le_right bot_le
#align bot_sup_eq bot_sup_eq

-- Porting note: Not simp because simp can prove it
theorem sup_bot_eq : a ‚äî ‚ä• = a :=
  sup_of_le_left bot_le
#align sup_bot_eq sup_bot_eq

@[simp]
theorem sup_eq_bot_iff : a ‚äî b = ‚ä• ‚Üî a = ‚ä• ‚àß b = ‚ä• := by rw [eq_bot_iff, sup_le_iff]; simp
                                                         -- ‚ä¢ a ‚â§ ‚ä• ‚àß b ‚â§ ‚ä• ‚Üî a = ‚ä• ‚àß b = ‚ä•
                                                                                      -- üéâ no goals
#align sup_eq_bot_iff sup_eq_bot_iff

end SemilatticeSupBot

section SemilatticeInfTop

variable [SemilatticeInf Œ±] [OrderTop Œ±] {a b : Œ±}

-- Porting note: Not simp because simp can prove it
theorem top_inf_eq : ‚ä§ ‚äì a = a :=
  inf_of_le_right le_top
#align top_inf_eq top_inf_eq

-- Porting note: Not simp because simp can prove it
theorem inf_top_eq : a ‚äì ‚ä§ = a :=
  inf_of_le_left le_top
#align inf_top_eq inf_top_eq

@[simp]
theorem inf_eq_top_iff : a ‚äì b = ‚ä§ ‚Üî a = ‚ä§ ‚àß b = ‚ä§ :=
  @sup_eq_bot_iff Œ±·µí·µà _ _ _ _
#align inf_eq_top_iff inf_eq_top_iff

end SemilatticeInfTop

section SemilatticeInfBot

variable [SemilatticeInf Œ±] [OrderBot Œ±] {a : Œ±}

-- Porting note: Not simp because simp can prove it
theorem bot_inf_eq : ‚ä• ‚äì a = ‚ä• :=
  inf_of_le_left bot_le
#align bot_inf_eq bot_inf_eq

-- Porting note: Not simp because simp can prove it
theorem inf_bot_eq : a ‚äì ‚ä• = ‚ä• :=
  inf_of_le_right bot_le
#align inf_bot_eq inf_bot_eq

end SemilatticeInfBot

/-! ### Bounded order -/


/-- A bounded order describes an order `(‚â§)` with a top and bottom element,
  denoted `‚ä§` and `‚ä•` respectively. -/
class BoundedOrder (Œ± : Type u) [LE Œ±] extends OrderTop Œ±, OrderBot Œ±
#align bounded_order BoundedOrder

instance OrderDual.boundedOrder (Œ± : Type u) [LE Œ±] [BoundedOrder Œ±] : BoundedOrder Œ±·µí·µà where
  __ := inferInstanceAs (OrderTop Œ±·µí·µà)
  __ := inferInstanceAs (OrderBot Œ±·µí·µà)

theorem BoundedOrder.ext {Œ±} [PartialOrder Œ±] {A B : BoundedOrder Œ±} : A = B := by
  have ht : @BoundedOrder.toOrderTop Œ± _ A = @BoundedOrder.toOrderTop Œ± _ B := OrderTop.ext
  -- ‚ä¢ A = B
  have hb : @BoundedOrder.toOrderBot Œ± _ A = @BoundedOrder.toOrderBot Œ± _ B := OrderBot.ext
  -- ‚ä¢ A = B
  cases A
  -- ‚ä¢ mk = B
  cases B
  -- ‚ä¢ mk = mk
  congr
  -- üéâ no goals
#align bounded_order.ext BoundedOrder.ext

section Logic

/-!
#### In this section we prove some properties about monotone and antitone operations on `Prop`
-/


section Preorder

variable [Preorder Œ±]

theorem monotone_and {p q : Œ± ‚Üí Prop} (m_p : Monotone p) (m_q : Monotone q) :
    Monotone fun x => p x ‚àß q x :=
  fun _ _ h => And.imp (m_p h) (m_q h)
#align monotone_and monotone_and

-- Note: by finish [monotone] doesn't work
theorem monotone_or {p q : Œ± ‚Üí Prop} (m_p : Monotone p) (m_q : Monotone q) :
    Monotone fun x => p x ‚à® q x :=
  fun _ _ h => Or.imp (m_p h) (m_q h)
#align monotone_or monotone_or

theorem monotone_le {x : Œ±} : Monotone ((¬∑ ‚â§ ¬∑) x) := fun _ _ h' h => h.trans h'
#align monotone_le monotone_le

theorem monotone_lt {x : Œ±} : Monotone ((¬∑ < ¬∑) x) := fun _ _ h' h => h.trans_le h'
#align monotone_lt monotone_lt

theorem antitone_le {x : Œ±} : Antitone (¬∑ ‚â§ x) := fun _ _ h' h => h'.trans h
#align antitone_le antitone_le

theorem antitone_lt {x : Œ±} : Antitone (¬∑ < x) := fun _ _ h' h => h'.trans_lt h
#align antitone_lt antitone_lt

theorem Monotone.forall {P : Œ≤ ‚Üí Œ± ‚Üí Prop} (hP : ‚àÄ x, Monotone (P x)) :
    Monotone fun y => ‚àÄ x, P x y :=
  fun _ _ hy h x => hP x hy <| h x
#align monotone.forall Monotone.forall

theorem Antitone.forall {P : Œ≤ ‚Üí Œ± ‚Üí Prop} (hP : ‚àÄ x, Antitone (P x)) :
    Antitone fun y => ‚àÄ x, P x y :=
  fun _ _ hy h x => hP x hy (h x)
#align antitone.forall Antitone.forall

theorem Monotone.ball {P : Œ≤ ‚Üí Œ± ‚Üí Prop} {s : Set Œ≤} (hP : ‚àÄ x ‚àà s, Monotone (P x)) :
    Monotone fun y => ‚àÄ x ‚àà s, P x y := fun _ _ hy h x hx => hP x hx hy (h x hx)
#align monotone.ball Monotone.ball

theorem Antitone.ball {P : Œ≤ ‚Üí Œ± ‚Üí Prop} {s : Set Œ≤} (hP : ‚àÄ x ‚àà s, Antitone (P x)) :
    Antitone fun y => ‚àÄ x ‚àà s, P x y := fun _ _ hy h x hx => hP x hx hy (h x hx)
#align antitone.ball Antitone.ball

theorem Monotone.exists {P : Œ≤ ‚Üí Œ± ‚Üí Prop} (hP : ‚àÄ x, Monotone (P x)) :
    Monotone fun y => ‚àÉ x, P x y :=
  fun _ _ hy ‚ü®x, hx‚ü© ‚Ü¶ ‚ü®x, hP x hy hx‚ü©

theorem Antitone.exists {P : Œ≤ ‚Üí Œ± ‚Üí Prop} (hP : ‚àÄ x, Antitone (P x)) :
    Antitone fun y => ‚àÉ x, P x y :=
  fun _ _ hy ‚ü®x, hx‚ü© ‚Ü¶ ‚ü®x, hP x hy hx‚ü©

theorem forall_ge_iff {P : Œ± ‚Üí Prop} {x‚ÇÄ : Œ±} (hP : Monotone P) :
    (‚àÄ x ‚â• x‚ÇÄ, P x) ‚Üî P x‚ÇÄ :=
  ‚ü®fun H ‚Ü¶ H x‚ÇÄ le_rfl, fun H _ hx ‚Ü¶ hP hx H‚ü©

theorem forall_le_iff {P : Œ± ‚Üí Prop} {x‚ÇÄ : Œ±} (hP : Antitone P) :
    (‚àÄ x ‚â§ x‚ÇÄ, P x) ‚Üî P x‚ÇÄ :=
  ‚ü®fun H ‚Ü¶ H x‚ÇÄ le_rfl, fun H _ hx ‚Ü¶ hP hx H‚ü©

end Preorder

section SemilatticeSup

variable [SemilatticeSup Œ±]

theorem exists_ge_and_iff_exists {P : Œ± ‚Üí Prop} {x‚ÇÄ : Œ±} (hP : Monotone P) :
    (‚àÉ x, x‚ÇÄ ‚â§ x ‚àß P x) ‚Üî ‚àÉ x, P x :=
  ‚ü®fun h => h.imp fun _ h => h.2, fun ‚ü®x, hx‚ü© => ‚ü®x ‚äî x‚ÇÄ, le_sup_right, hP le_sup_left hx‚ü©‚ü©
#align exists_ge_and_iff_exists exists_ge_and_iff_exists

end SemilatticeSup

section SemilatticeInf

variable [SemilatticeInf Œ±]

theorem exists_le_and_iff_exists {P : Œ± ‚Üí Prop} {x‚ÇÄ : Œ±} (hP : Antitone P) :
    (‚àÉ x, x ‚â§ x‚ÇÄ ‚àß P x) ‚Üî ‚àÉ x, P x :=
  exists_ge_and_iff_exists <| hP.dual_left
#align exists_le_and_iff_exists exists_le_and_iff_exists

end SemilatticeInf

end Logic

/-! ### Function lattices -/


namespace Pi

variable {Œπ : Type*} {Œ±' : Œπ ‚Üí Type*}

instance [‚àÄ i, Bot (Œ±' i)] : Bot (‚àÄ i, Œ±' i) :=
  ‚ü®fun _ => ‚ä•‚ü©

@[simp]
theorem bot_apply [‚àÄ i, Bot (Œ±' i)] (i : Œπ) : (‚ä• : ‚àÄ i, Œ±' i) i = ‚ä• :=
  rfl
#align pi.bot_apply Pi.bot_apply

theorem bot_def [‚àÄ i, Bot (Œ±' i)] : (‚ä• : ‚àÄ i, Œ±' i) = fun _ => ‚ä• :=
  rfl
#align pi.bot_def Pi.bot_def

instance [‚àÄ i, Top (Œ±' i)] : Top (‚àÄ i, Œ±' i) :=
  ‚ü®fun _ => ‚ä§‚ü©

@[simp]
theorem top_apply [‚àÄ i, Top (Œ±' i)] (i : Œπ) : (‚ä§ : ‚àÄ i, Œ±' i) i = ‚ä§ :=
  rfl
#align pi.top_apply Pi.top_apply

theorem top_def [‚àÄ i, Top (Œ±' i)] : (‚ä§ : ‚àÄ i, Œ±' i) = fun _ => ‚ä§ :=
  rfl
#align pi.top_def Pi.top_def

instance orderTop [‚àÄ i, LE (Œ±' i)] [‚àÄ i, OrderTop (Œ±' i)] : OrderTop (‚àÄ i, Œ±' i) :=
  { inferInstanceAs (Top (‚àÄ i, Œ±' i)) with le_top := fun _ _ => le_top }

instance orderBot [‚àÄ i, LE (Œ±' i)] [‚àÄ i, OrderBot (Œ±' i)] : OrderBot (‚àÄ i, Œ±' i) :=
  { inferInstanceAs (Bot (‚àÄ i, Œ±' i)) with bot_le := fun _ _ => bot_le }

instance boundedOrder [‚àÄ i, LE (Œ±' i)] [‚àÄ i, BoundedOrder (Œ±' i)] : BoundedOrder (‚àÄ i, Œ±' i) where
  __ := inferInstanceAs (OrderTop (‚àÄ i, Œ±' i))
  __ := inferInstanceAs (OrderBot (‚àÄ i, Œ±' i))

end Pi

section Subsingleton

variable [PartialOrder Œ±] [BoundedOrder Œ±]

theorem eq_bot_of_bot_eq_top (hŒ± : (‚ä• : Œ±) = ‚ä§) (x : Œ±) : x = (‚ä• : Œ±) :=
  eq_bot_mono le_top (Eq.symm hŒ±)
#align eq_bot_of_bot_eq_top eq_bot_of_bot_eq_top

theorem eq_top_of_bot_eq_top (hŒ± : (‚ä• : Œ±) = ‚ä§) (x : Œ±) : x = (‚ä§ : Œ±) :=
  eq_top_mono bot_le hŒ±
#align eq_top_of_bot_eq_top eq_top_of_bot_eq_top

theorem subsingleton_of_top_le_bot (h : (‚ä§ : Œ±) ‚â§ (‚ä• : Œ±)) : Subsingleton Œ± :=
  ‚ü®fun _ _ => le_antisymm
    (le_trans le_top <| le_trans h bot_le) (le_trans le_top <| le_trans h bot_le)‚ü©
#align subsingleton_of_top_le_bot subsingleton_of_top_le_bot

theorem subsingleton_of_bot_eq_top (hŒ± : (‚ä• : Œ±) = (‚ä§ : Œ±)) : Subsingleton Œ± :=
  subsingleton_of_top_le_bot (ge_of_eq hŒ±)
#align subsingleton_of_bot_eq_top subsingleton_of_bot_eq_top

theorem subsingleton_iff_bot_eq_top : (‚ä• : Œ±) = (‚ä§ : Œ±) ‚Üî Subsingleton Œ± :=
  ‚ü®subsingleton_of_bot_eq_top, fun _ => Subsingleton.elim ‚ä• ‚ä§‚ü©
#align subsingleton_iff_bot_eq_top subsingleton_iff_bot_eq_top

end Subsingleton

section lift

-- See note [reducible non-instances]
/-- Pullback an `OrderTop`. -/
@[reducible]
def OrderTop.lift [LE Œ±] [Top Œ±] [LE Œ≤] [OrderTop Œ≤] (f : Œ± ‚Üí Œ≤)
    (map_le : ‚àÄ a b, f a ‚â§ f b ‚Üí a ‚â§ b) (map_top : f ‚ä§ = ‚ä§) : OrderTop Œ± :=
  ‚ü®fun a =>
    map_le _ _ <| by
      rw [map_top]
      -- ‚ä¢ f a ‚â§ ‚ä§
      -- Porting note: lean3 didn't need the type annotation
      exact @le_top Œ≤ _ _ _‚ü©
      -- üéâ no goals
#align order_top.lift OrderTop.lift

-- See note [reducible non-instances]
/-- Pullback an `OrderBot`. -/
@[reducible]
def OrderBot.lift [LE Œ±] [Bot Œ±] [LE Œ≤] [OrderBot Œ≤] (f : Œ± ‚Üí Œ≤)
    (map_le : ‚àÄ a b, f a ‚â§ f b ‚Üí a ‚â§ b) (map_bot : f ‚ä• = ‚ä•) : OrderBot Œ± :=
  ‚ü®fun a =>
    map_le _ _ <| by
      rw [map_bot]
      -- ‚ä¢ ‚ä• ‚â§ f a
      -- Porting note: lean3 didn't need the type annotation
      exact @bot_le Œ≤ _ _ _‚ü©
      -- üéâ no goals
#align order_bot.lift OrderBot.lift

-- See note [reducible non-instances]
/-- Pullback a `BoundedOrder`. -/
@[reducible]
def BoundedOrder.lift [LE Œ±] [Top Œ±] [Bot Œ±] [LE Œ≤] [BoundedOrder Œ≤] (f : Œ± ‚Üí Œ≤)
    (map_le : ‚àÄ a b, f a ‚â§ f b ‚Üí a ‚â§ b) (map_top : f ‚ä§ = ‚ä§) (map_bot : f ‚ä• = ‚ä•) : BoundedOrder Œ± :=
  { OrderTop.lift f map_le map_top, OrderBot.lift f map_le map_bot with }
#align bounded_order.lift BoundedOrder.lift

end lift

/-! ### Subtype, order dual, product lattices -/


namespace Subtype

variable {p : Œ± ‚Üí Prop}

-- See note [reducible non-instances]
/-- A subtype remains a `‚ä•`-order if the property holds at `‚ä•`. -/
@[reducible]
protected def orderBot [LE Œ±] [OrderBot Œ±] (hbot : p ‚ä•) : OrderBot { x : Œ± // p x } where
  bot := ‚ü®‚ä•, hbot‚ü©
  bot_le _ := bot_le
#align subtype.order_bot Subtype.orderBot

-- See note [reducible non-instances]
/-- A subtype remains a `‚ä§`-order if the property holds at `‚ä§`. -/
@[reducible]
protected def orderTop [LE Œ±] [OrderTop Œ±] (htop : p ‚ä§) : OrderTop { x : Œ± // p x } where
  top := ‚ü®‚ä§, htop‚ü©
  le_top _ := le_top
#align subtype.order_top Subtype.orderTop

-- See note [reducible non-instances]
/-- A subtype remains a bounded order if the property holds at `‚ä•` and `‚ä§`. -/
@[reducible]
protected def boundedOrder [LE Œ±] [BoundedOrder Œ±] (hbot : p ‚ä•) (htop : p ‚ä§) :
    BoundedOrder (Subtype p) :=
  { Subtype.orderTop htop, Subtype.orderBot hbot with }
#align subtype.bounded_order Subtype.boundedOrder

variable [PartialOrder Œ±]

@[simp]
theorem mk_bot [OrderBot Œ±] [OrderBot (Subtype p)] (hbot : p ‚ä•) : mk ‚ä• hbot = ‚ä• :=
  le_bot_iff.1 <| coe_le_coe.1 bot_le
#align subtype.mk_bot Subtype.mk_bot

@[simp]
theorem mk_top [OrderTop Œ±] [OrderTop (Subtype p)] (htop : p ‚ä§) : mk ‚ä§ htop = ‚ä§ :=
  top_le_iff.1 <| coe_le_coe.1 le_top
#align subtype.mk_top Subtype.mk_top

theorem coe_bot [OrderBot Œ±] [OrderBot (Subtype p)] (hbot : p ‚ä•) : ((‚ä• : Subtype p) : Œ±) = ‚ä• :=
  congr_arg Subtype.val (mk_bot hbot).symm
#align subtype.coe_bot Subtype.coe_bot

theorem coe_top [OrderTop Œ±] [OrderTop (Subtype p)] (htop : p ‚ä§) : ((‚ä§ : Subtype p) : Œ±) = ‚ä§ :=
  congr_arg Subtype.val (mk_top htop).symm
#align subtype.coe_top Subtype.coe_top

@[simp]
theorem coe_eq_bot_iff [OrderBot Œ±] [OrderBot (Subtype p)] (hbot : p ‚ä•) {x : { x // p x }} :
    (x : Œ±) = ‚ä• ‚Üî x = ‚ä• := by
  rw [‚Üê coe_bot hbot, ext_iff]
  -- üéâ no goals
#align subtype.coe_eq_bot_iff Subtype.coe_eq_bot_iff

@[simp]
theorem coe_eq_top_iff [OrderTop Œ±] [OrderTop (Subtype p)] (htop : p ‚ä§) {x : { x // p x }} :
    (x : Œ±) = ‚ä§ ‚Üî x = ‚ä§ := by
  rw [‚Üê coe_top htop, ext_iff]
  -- üéâ no goals
#align subtype.coe_eq_top_iff Subtype.coe_eq_top_iff

@[simp]
theorem mk_eq_bot_iff [OrderBot Œ±] [OrderBot (Subtype p)] (hbot : p ‚ä•) {x : Œ±} (hx : p x) :
    (‚ü®x, hx‚ü© : Subtype p) = ‚ä• ‚Üî x = ‚ä• :=
  (coe_eq_bot_iff hbot).symm
#align subtype.mk_eq_bot_iff Subtype.mk_eq_bot_iff

@[simp]
theorem mk_eq_top_iff [OrderTop Œ±] [OrderTop (Subtype p)] (htop : p ‚ä§) {x : Œ±} (hx : p x) :
    (‚ü®x, hx‚ü© : Subtype p) = ‚ä§ ‚Üî x = ‚ä§ :=
  (coe_eq_top_iff htop).symm
#align subtype.mk_eq_top_iff Subtype.mk_eq_top_iff

end Subtype

namespace Prod

variable (Œ± Œ≤)

instance top [Top Œ±] [Top Œ≤] : Top (Œ± √ó Œ≤) :=
  ‚ü®‚ü®‚ä§, ‚ä§‚ü©‚ü©

instance bot [Bot Œ±] [Bot Œ≤] : Bot (Œ± √ó Œ≤) :=
  ‚ü®‚ü®‚ä•, ‚ä•‚ü©‚ü©

theorem fst_top [Top Œ±] [Top Œ≤] : (‚ä§ : Œ± √ó Œ≤).fst = ‚ä§ := rfl
theorem snd_top [Top Œ±] [Top Œ≤] : (‚ä§ : Œ± √ó Œ≤).snd = ‚ä§ := rfl
theorem fst_bot [Bot Œ±] [Bot Œ≤] : (‚ä• : Œ± √ó Œ≤).fst = ‚ä• := rfl
theorem snd_bot [Bot Œ±] [Bot Œ≤] : (‚ä• : Œ± √ó Œ≤).snd = ‚ä• := rfl

instance orderTop [LE Œ±] [LE Œ≤] [OrderTop Œ±] [OrderTop Œ≤] : OrderTop (Œ± √ó Œ≤) where
  __ := inferInstanceAs (Top (Œ± √ó Œ≤))
  le_top _ := ‚ü®le_top, le_top‚ü©

instance orderBot [LE Œ±] [LE Œ≤] [OrderBot Œ±] [OrderBot Œ≤] : OrderBot (Œ± √ó Œ≤) where
  __ := inferInstanceAs (Bot (Œ± √ó Œ≤))
  bot_le _ := ‚ü®bot_le, bot_le‚ü©

instance boundedOrder [LE Œ±] [LE Œ≤] [BoundedOrder Œ±] [BoundedOrder Œ≤] : BoundedOrder (Œ± √ó Œ≤) where
  __ := inferInstanceAs (OrderTop (Œ± √ó Œ≤))
  __ := inferInstanceAs (OrderBot (Œ± √ó Œ≤))

end Prod

namespace ULift

instance [Top Œ±] : Top (ULift.{v} Œ±) where top := up ‚ä§

@[simp] theorem up_top [Top Œ±] : up (‚ä§ : Œ±) = ‚ä§ := rfl
@[simp] theorem down_top [Top Œ±] : down (‚ä§ : ULift Œ±) = ‚ä§ := rfl

instance [Bot Œ±] : Bot (ULift.{v} Œ±) where bot := up ‚ä•

@[simp] theorem up_bot [Bot Œ±] : up (‚ä• : Œ±) = ‚ä• := rfl
@[simp] theorem down_bot [Bot Œ±] : down (‚ä• : ULift Œ±) = ‚ä• := rfl

instance [LE Œ±] [OrderBot Œ±] : OrderBot (ULift.{v} Œ±) :=
  OrderBot.lift ULift.down (fun _ _ => down_le.mp) down_bot

instance [LE Œ±] [OrderTop Œ±] : OrderTop (ULift.{v} Œ±) :=
  OrderTop.lift ULift.down (fun _ _ => down_le.mp) down_top

instance [LE Œ±] [BoundedOrder Œ±] : BoundedOrder (ULift.{v} Œ±) where

end ULift

section LinearOrder

variable [LinearOrder Œ±]

-- `simp` can prove these, so they shouldn't be simp-lemmas.
theorem min_bot_left [OrderBot Œ±] (a : Œ±) : min ‚ä• a = ‚ä• :=
  bot_inf_eq
#align min_bot_left min_bot_left

theorem max_top_left [OrderTop Œ±] (a : Œ±) : max ‚ä§ a = ‚ä§ :=
  top_sup_eq
#align max_top_left max_top_left

theorem min_top_left [OrderTop Œ±] (a : Œ±) : min ‚ä§ a = a :=
  top_inf_eq
#align min_top_left min_top_left

theorem max_bot_left [OrderBot Œ±] (a : Œ±) : max ‚ä• a = a :=
  bot_sup_eq
#align max_bot_left max_bot_left

theorem min_top_right [OrderTop Œ±] (a : Œ±) : min a ‚ä§ = a :=
  inf_top_eq
#align min_top_right min_top_right

theorem max_bot_right [OrderBot Œ±] (a : Œ±) : max a ‚ä• = a :=
  sup_bot_eq
#align max_bot_right max_bot_right

theorem min_bot_right [OrderBot Œ±] (a : Œ±) : min a ‚ä• = ‚ä• :=
  inf_bot_eq
#align min_bot_right min_bot_right

theorem max_top_right [OrderTop Œ±] (a : Œ±) : max a ‚ä§ = ‚ä§ :=
  sup_top_eq
#align max_top_right max_top_right

@[simp]
theorem min_eq_bot [OrderBot Œ±] {a b : Œ±} : min a b = ‚ä• ‚Üî a = ‚ä• ‚à® b = ‚ä• := by
  simp only [‚Üê inf_eq_min, ‚Üê le_bot_iff, inf_le_iff]
  -- üéâ no goals
#align min_eq_bot min_eq_bot

@[simp]
theorem max_eq_top [OrderTop Œ±] {a b : Œ±} : max a b = ‚ä§ ‚Üî a = ‚ä§ ‚à® b = ‚ä§ :=
  @min_eq_bot Œ±·µí·µà _ _ a b
#align max_eq_top max_eq_top

@[simp]
theorem max_eq_bot [OrderBot Œ±] {a b : Œ±} : max a b = ‚ä• ‚Üî a = ‚ä• ‚àß b = ‚ä• :=
  sup_eq_bot_iff
#align max_eq_bot max_eq_bot

@[simp]
theorem min_eq_top [OrderTop Œ±] {a b : Œ±} : min a b = ‚ä§ ‚Üî a = ‚ä§ ‚àß b = ‚ä§ :=
  inf_eq_top_iff
#align min_eq_top min_eq_top

end LinearOrder

section Nontrivial

variable [PartialOrder Œ±] [BoundedOrder Œ±] [Nontrivial Œ±]

@[simp]
theorem bot_ne_top : (‚ä• : Œ±) ‚â† ‚ä§ := fun h => not_subsingleton _ <| subsingleton_of_bot_eq_top h
#align bot_ne_top bot_ne_top

@[simp]
theorem top_ne_bot : (‚ä§ : Œ±) ‚â† ‚ä• :=
  bot_ne_top.symm
#align top_ne_bot top_ne_bot

@[simp]
theorem bot_lt_top : (‚ä• : Œ±) < ‚ä§ :=
  lt_top_iff_ne_top.2 bot_ne_top
#align bot_lt_top bot_lt_top

end Nontrivial

section Bool

open Bool

instance Bool.boundedOrder : BoundedOrder Bool where
  top := true
  le_top _ := le_true
  bot := false
  bot_le _ := false_le

@[simp]
theorem top_eq_true : ‚ä§ = true :=
  rfl
#align top_eq_tt top_eq_true

@[simp]
theorem bot_eq_false : ‚ä• = false :=
  rfl
#align bot_eq_ff bot_eq_false

end Bool
