/-
Copyright (c) 2020 David W√§rn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David W√§rn
-/
import Mathlib.Order.Ideal
import Mathlib.Data.Finset.Lattice

#align_import order.countable_dense_linear_order from "leanprover-community/mathlib"@"2705404e701abc6b3127da906f40bae062a169c9"

/-!
# The back and forth method and countable dense linear orders

## Results

Suppose `Œ± Œ≤` are linear orders, with `Œ±` countable and `Œ≤` dense, nontrivial. Then there is an
order embedding `Œ± ‚Ü™ Œ≤`. If in addition `Œ±` is dense, nonempty, without endpoints and `Œ≤` is
countable, without endpoints, then we can upgrade this to an order isomorphism `Œ± ‚âÉ Œ≤`.

The idea for both results is to consider "partial isomorphisms", which identify a finite subset of
`Œ±` with a finite subset of `Œ≤`, and prove that for any such partial isomorphism `f` and `a : Œ±`, we
can extend `f` to include `a` in its domain.

## References

https://en.wikipedia.org/wiki/Back-and-forth_method

## Tags

back and forth, dense, countable, order
-/


noncomputable section

open Classical

namespace Order

/-- Suppose `Œ±` is a nonempty dense linear order without endpoints, and
    suppose `lo`, `hi`, are finite subsets with all of `lo` strictly
    before `hi`. Then there is an element of `Œ±` strictly between `lo`
    and `hi`. -/
theorem exists_between_finsets {Œ± : Type*} [LinearOrder Œ±] [DenselyOrdered Œ±] [NoMinOrder Œ±]
    [NoMaxOrder Œ±] [nonem : Nonempty Œ±] (lo hi : Finset Œ±) (lo_lt_hi : ‚àÄ x ‚àà lo, ‚àÄ y ‚àà hi, x < y) :
    ‚àÉ m : Œ±, (‚àÄ x ‚àà lo, x < m) ‚àß ‚àÄ y ‚àà hi, m < y :=
  if nlo : lo.Nonempty then
    if nhi : hi.Nonempty then
      -- both sets are nonempty, use densely_ordered
        Exists.elim
        (exists_between (lo_lt_hi _ (Finset.max'_mem _ nlo) _ (Finset.min'_mem _ nhi))) fun m hm ‚Ü¶
        ‚ü®m, fun x hx ‚Ü¶ lt_of_le_of_lt (Finset.le_max' lo x hx) hm.1, fun y hy ‚Ü¶
          lt_of_lt_of_le hm.2 (Finset.min'_le hi y hy)‚ü©
    else-- upper set is empty, use `NoMaxOrder`
        Exists.elim
        (exists_gt (Finset.max' lo nlo)) fun m hm ‚Ü¶
        ‚ü®m, fun x hx ‚Ü¶ lt_of_le_of_lt (Finset.le_max' lo x hx) hm, fun y hy ‚Ü¶ (nhi ‚ü®y, hy‚ü©).elim‚ü©
  else
    if nhi : hi.Nonempty then
      -- lower set is empty, use `NoMinOrder`
        Exists.elim
        (exists_lt (Finset.min' hi nhi)) fun m hm ‚Ü¶
        ‚ü®m, fun x hx ‚Ü¶ (nlo ‚ü®x, hx‚ü©).elim, fun y hy ‚Ü¶ lt_of_lt_of_le hm (Finset.min'_le hi y hy)‚ü©
    else-- both sets are empty, use nonempty
          nonem.elim
        fun m ‚Ü¶ ‚ü®m, fun x hx ‚Ü¶ (nlo ‚ü®x, hx‚ü©).elim, fun y hy ‚Ü¶ (nhi ‚ü®y, hy‚ü©).elim‚ü©
#align order.exists_between_finsets Order.exists_between_finsets

variable (Œ± Œ≤ : Type*) [LinearOrder Œ±] [LinearOrder Œ≤]

-- Porting note: Mathport warning: expanding binder collection (p q ¬´expr ‚àà ¬ª f)
/-- The type of partial order isomorphisms between `Œ±` and `Œ≤` defined on finite subsets.
    A partial order isomorphism is encoded as a finite subset of `Œ± √ó Œ≤`, consisting
    of pairs which should be identified. -/
def PartialIso : Type _ :=
  { f : Finset (Œ± √ó Œ≤) //
    ‚àÄ (p) (_ : p ‚àà f) (q) (_ : q ‚àà f),
      cmp (Prod.fst p) (Prod.fst q) = cmp (Prod.snd p) (Prod.snd q) }
#align order.partial_iso Order.PartialIso

namespace PartialIso

instance : Inhabited (PartialIso Œ± Œ≤) := ‚ü®‚ü®‚àÖ, fun _p h _q ‚Ü¶ (Finset.not_mem_empty _ h).elim‚ü©‚ü©

instance : Preorder (PartialIso Œ± Œ≤) := Subtype.preorder _

variable {Œ± Œ≤}

/-- For each `a`, we can find a `b` in the codomain, such that `a`'s relation to
the domain of `f` is `b`'s relation to the image of `f`.

Thus, if `a` is not already in `f`, then we can extend `f` by sending `a` to `b`.
-/
theorem exists_across [DenselyOrdered Œ≤] [NoMinOrder Œ≤] [NoMaxOrder Œ≤] [Nonempty Œ≤]
    (f : PartialIso Œ± Œ≤) (a : Œ±) :
    ‚àÉ b : Œ≤, ‚àÄ p ‚àà f.val, cmp (Prod.fst p) a = cmp (Prod.snd p) b := by
  by_cases h : ‚àÉ b, (a, b) ‚àà f.val
  -- ‚ä¢ ‚àÉ b, ‚àÄ (p : Œ± √ó Œ≤), p ‚àà ‚Üëf ‚Üí cmp p.fst a = cmp p.snd b
  ¬∑ cases' h with b hb
    -- ‚ä¢ ‚àÉ b, ‚àÄ (p : Œ± √ó Œ≤), p ‚àà ‚Üëf ‚Üí cmp p.fst a = cmp p.snd b
    exact ‚ü®b, fun p hp ‚Ü¶ f.prop _ hp _ hb‚ü©
    -- üéâ no goals
  have :
    ‚àÄ x ‚àà (f.val.filter fun p : Œ± √ó Œ≤ ‚Ü¶ p.fst < a).image Prod.snd,
      ‚àÄ y ‚àà (f.val.filter fun p : Œ± √ó Œ≤ ‚Ü¶ a < p.fst).image Prod.snd, x < y := by
    intro x hx y hy
    rw [Finset.mem_image] at hx hy
    rcases hx with ‚ü®p, hp1, rfl‚ü©
    rcases hy with ‚ü®q, hq1, rfl‚ü©
    rw [Finset.mem_filter] at hp1 hq1
    rw [‚Üê lt_iff_lt_of_cmp_eq_cmp (f.prop _ hp1.1 _ hq1.1)]
    exact lt_trans hp1.right hq1.right
  cases' exists_between_finsets _ _ this with b hb
  -- ‚ä¢ ‚àÉ b, ‚àÄ (p : Œ± √ó Œ≤), p ‚àà ‚Üëf ‚Üí cmp p.fst a = cmp p.snd b
  use b
  -- ‚ä¢ ‚àÄ (p : Œ± √ó Œ≤), p ‚àà ‚Üëf ‚Üí cmp p.fst a = cmp p.snd b
  rintro ‚ü®p1, p2‚ü© hp
  -- ‚ä¢ cmp (p1, p2).fst a = cmp (p1, p2).snd b
  have : p1 ‚â† a := fun he ‚Ü¶ h ‚ü®p2, he ‚ñ∏ hp‚ü©
  -- ‚ä¢ cmp (p1, p2).fst a = cmp (p1, p2).snd b
  cases' lt_or_gt_of_ne this with hl hr
  -- ‚ä¢ cmp (p1, p2).fst a = cmp (p1, p2).snd b
  ¬∑ have : p1 < a ‚àß p2 < b :=
      ‚ü®hl, hb.1 _ (Finset.mem_image.mpr ‚ü®(p1, p2), Finset.mem_filter.mpr ‚ü®hp, hl‚ü©, rfl‚ü©)‚ü©
    rw [‚Üê cmp_eq_lt_iff, ‚Üê cmp_eq_lt_iff] at this
    -- ‚ä¢ cmp (p1, p2).fst a = cmp (p1, p2).snd b
    exact this.1.trans this.2.symm
    -- üéâ no goals
  ¬∑ have : a < p1 ‚àß b < p2 :=
      ‚ü®hr, hb.2 _ (Finset.mem_image.mpr ‚ü®(p1, p2), Finset.mem_filter.mpr ‚ü®hp, hr‚ü©, rfl‚ü©)‚ü©
    rw [‚Üê cmp_eq_gt_iff, ‚Üê cmp_eq_gt_iff] at this
    -- ‚ä¢ cmp (p1, p2).fst a = cmp (p1, p2).snd b
    exact this.1.trans this.2.symm
    -- üéâ no goals
#align order.partial_iso.exists_across Order.PartialIso.exists_across

/-- A partial isomorphism between `Œ±` and `Œ≤` is also a partial isomorphism between `Œ≤` and `Œ±`. -/
protected def comm : PartialIso Œ± Œ≤ ‚Üí PartialIso Œ≤ Œ± :=
  Subtype.map (Finset.image (Equiv.prodComm _ _)) fun f hf p hp q hq ‚Ü¶
    Eq.symm <|
      hf ((Equiv.prodComm Œ± Œ≤).symm p)
        (by
          rw [‚Üê Finset.mem_coe, Finset.coe_image, Equiv.image_eq_preimage] at hp
          -- ‚ä¢ ‚Üë(Equiv.prodComm Œ± Œ≤).symm p ‚àà f
          rwa [‚Üê Finset.mem_coe])
          -- üéâ no goals
        ((Equiv.prodComm Œ± Œ≤).symm q)
        (by
          rw [‚Üê Finset.mem_coe, Finset.coe_image, Equiv.image_eq_preimage] at hq
          -- ‚ä¢ ‚Üë(Equiv.prodComm Œ± Œ≤).symm q ‚àà f
          rwa [‚Üê Finset.mem_coe])
          -- üéâ no goals
#align order.partial_iso.comm Order.PartialIso.comm

variable (Œ≤)

/-- The set of partial isomorphisms defined at `a : Œ±`, together with a proof that any
    partial isomorphism can be extended to one defined at `a`. -/
def definedAtLeft [DenselyOrdered Œ≤] [NoMinOrder Œ≤] [NoMaxOrder Œ≤] [Nonempty Œ≤] (a : Œ±) :
    Cofinal (PartialIso Œ± Œ≤) where
  carrier := {f | ‚àÉ b : Œ≤, (a, b) ‚àà f.val}
  mem_gt f := by
    cases' exists_across f a with b a_b
    -- ‚ä¢ ‚àÉ y, y ‚àà {f | ‚àÉ b, (a, b) ‚àà ‚Üëf} ‚àß f ‚â§ y
    refine
      ‚ü®‚ü®insert (a, b) f.val, fun p hp q hq ‚Ü¶ ?_‚ü©, ‚ü®b, Finset.mem_insert_self _ _‚ü©,
        Finset.subset_insert _ _‚ü©
    rw [Finset.mem_insert] at hp hq
    -- ‚ä¢ cmp p.fst q.fst = cmp p.snd q.snd
    rcases hp with (rfl | pf) <;> rcases hq with (rfl | qf)
    -- ‚ä¢ cmp (a, b).fst q.fst = cmp (a, b).snd q.snd
                                  -- ‚ä¢ cmp (a, b).fst (a, b).fst = cmp (a, b).snd (a, b).snd
                                  -- ‚ä¢ cmp p.fst (a, b).fst = cmp p.snd (a, b).snd
    ¬∑ simp only [cmp_self_eq_eq]
      -- üéâ no goals
    ¬∑ rw [cmp_eq_cmp_symm]
      -- ‚ä¢ cmp q.fst (a, b).fst = cmp q.snd (a, b).snd
      exact a_b _ qf
      -- üéâ no goals
    ¬∑ exact a_b _ pf
      -- üéâ no goals
    ¬∑ exact f.prop _ pf _ qf
      -- üéâ no goals
#align order.partial_iso.defined_at_left Order.PartialIso.definedAtLeft

variable (Œ±) {Œ≤}

/-- The set of partial isomorphisms defined at `b : Œ≤`, together with a proof that any
    partial isomorphism can be extended to include `b`. We prove this by symmetry. -/
def definedAtRight [DenselyOrdered Œ±] [NoMinOrder Œ±] [NoMaxOrder Œ±] [Nonempty Œ±] (b : Œ≤) :
    Cofinal (PartialIso Œ± Œ≤) where
  carrier := {f | ‚àÉ a, (a, b) ‚àà f.val}
  mem_gt f := by
    rcases (definedAtLeft Œ± b).mem_gt f.comm with ‚ü®f', ‚ü®a, ha‚ü©, hl‚ü©
    -- ‚ä¢ ‚àÉ y, y ‚àà {f | ‚àÉ a, (a, b) ‚àà ‚Üëf} ‚àß f ‚â§ y
    refine' ‚ü®f'.comm, ‚ü®a, _‚ü©, _‚ü©
    -- ‚ä¢ (a, b) ‚àà ‚Üë(PartialIso.comm f')
    ¬∑ change (a, b) ‚àà f'.val.image _
      -- ‚ä¢ (a, b) ‚àà Finset.image ‚Üë(Equiv.prodComm Œ≤ Œ±) ‚Üëf'
      rwa [‚Üê Finset.mem_coe, Finset.coe_image, Equiv.image_eq_preimage]
      -- üéâ no goals
    ¬∑ change _ ‚äÜ f'.val.image _
      -- ‚ä¢ (fun a => ‚Üëa) f ‚äÜ Finset.image ‚Üë(Equiv.prodComm Œ≤ Œ±) ‚Üëf'
      rwa [‚Üê Finset.coe_subset, Finset.coe_image, ‚Üê Equiv.subset_image, ‚Üê Finset.coe_image,
        Finset.coe_subset]
#align order.partial_iso.defined_at_right Order.PartialIso.definedAtRight

variable {Œ±}

/-- Given an ideal which intersects `definedAtLeft Œ≤ a`, pick `b : Œ≤` such that
    some partial function in the ideal maps `a` to `b`. -/
def funOfIdeal [DenselyOrdered Œ≤] [NoMinOrder Œ≤] [NoMaxOrder Œ≤] [Nonempty Œ≤] (a : Œ±)
    (I : Ideal (PartialIso Œ± Œ≤)) :
    (‚àÉ f, f ‚àà definedAtLeft Œ≤ a ‚àß f ‚àà I) ‚Üí { b // ‚àÉ f ‚àà I, (a, b) ‚àà Subtype.val f } :=
  Classical.indefiniteDescription _ ‚àò fun ‚ü®f, ‚ü®b, hb‚ü©, hf‚ü© ‚Ü¶ ‚ü®b, f, hf, hb‚ü©
#align order.partial_iso.fun_of_ideal Order.PartialIso.funOfIdeal

/-- Given an ideal which intersects `definedAtRight Œ± b`, pick `a : Œ±` such that
    some partial function in the ideal maps `a` to `b`. -/
def invOfIdeal [DenselyOrdered Œ±] [NoMinOrder Œ±] [NoMaxOrder Œ±] [Nonempty Œ±] (b : Œ≤)
    (I : Ideal (PartialIso Œ± Œ≤)) :
    (‚àÉ f, f ‚àà definedAtRight Œ± b ‚àß f ‚àà I) ‚Üí { a // ‚àÉ f ‚àà I, (a, b) ‚àà Subtype.val f } :=
  Classical.indefiniteDescription _ ‚àò fun ‚ü®f, ‚ü®a, ha‚ü©, hf‚ü© ‚Ü¶ ‚ü®a, f, hf, ha‚ü©
#align order.partial_iso.inv_of_ideal Order.PartialIso.invOfIdeal

end PartialIso

open PartialIso

-- variable (Œ± Œ≤)

/-- Any countable linear order embeds in any nontrivial dense linear order. -/
theorem embedding_from_countable_to_dense [Encodable Œ±] [DenselyOrdered Œ≤] [Nontrivial Œ≤] :
    Nonempty (Œ± ‚Ü™o Œ≤) := by
  rcases exists_pair_lt Œ≤ with ‚ü®x, y, hxy‚ü©
  -- ‚ä¢ Nonempty (Œ± ‚Ü™o Œ≤)
  cases' exists_between hxy with a ha
  -- ‚ä¢ Nonempty (Œ± ‚Ü™o Œ≤)
  haveI : Nonempty (Set.Ioo x y) := ‚ü®‚ü®a, ha‚ü©‚ü©
  -- ‚ä¢ Nonempty (Œ± ‚Ü™o Œ≤)
  let our_ideal : Ideal (PartialIso Œ± _) :=
    idealOfCofinals default (definedAtLeft (Set.Ioo x y))
  let F a := funOfIdeal a our_ideal (cofinal_meets_idealOfCofinals _ _ a)
  -- ‚ä¢ Nonempty (Œ± ‚Ü™o Œ≤)
  refine
    ‚ü®RelEmbedding.trans (OrderEmbedding.ofStrictMono (fun a ‚Ü¶ (F a).val) fun a‚ÇÅ a‚ÇÇ ‚Ü¶ ?_)
        (OrderEmbedding.subtype _)‚ü©
  rcases(F a‚ÇÅ).prop with ‚ü®f, hf, ha‚ÇÅ‚ü©
  -- ‚ä¢ a‚ÇÅ < a‚ÇÇ ‚Üí (fun a => ‚Üë(F a)) a‚ÇÅ < (fun a => ‚Üë(F a)) a‚ÇÇ
  rcases(F a‚ÇÇ).prop with ‚ü®g, hg, ha‚ÇÇ‚ü©
  -- ‚ä¢ a‚ÇÅ < a‚ÇÇ ‚Üí (fun a => ‚Üë(F a)) a‚ÇÅ < (fun a => ‚Üë(F a)) a‚ÇÇ
  rcases our_ideal.directed _ hf _ hg with ‚ü®m, _hm, fm, gm‚ü©
  -- ‚ä¢ a‚ÇÅ < a‚ÇÇ ‚Üí (fun a => ‚Üë(F a)) a‚ÇÅ < (fun a => ‚Üë(F a)) a‚ÇÇ
  exact (lt_iff_lt_of_cmp_eq_cmp <| m.prop (a‚ÇÅ, _) (fm ha‚ÇÅ) (a‚ÇÇ, _) (gm ha‚ÇÇ)).mp
  -- üéâ no goals
#align order.embedding_from_countable_to_dense Order.embedding_from_countable_to_dense

/-- Any two countable dense, nonempty linear orders without endpoints are order isomorphic. -/
theorem iso_of_countable_dense [Encodable Œ±] [DenselyOrdered Œ±] [NoMinOrder Œ±] [NoMaxOrder Œ±]
    [Nonempty Œ±] [Encodable Œ≤] [DenselyOrdered Œ≤] [NoMinOrder Œ≤] [NoMaxOrder Œ≤] [Nonempty Œ≤] :
    Nonempty (Œ± ‚âÉo Œ≤) :=
  let to_cofinal : Sum Œ± Œ≤ ‚Üí Cofinal (PartialIso Œ± Œ≤) := fun p ‚Ü¶
    Sum.recOn p (definedAtLeft Œ≤) (definedAtRight Œ±)
  let our_ideal : Ideal (PartialIso Œ± Œ≤) := idealOfCofinals default to_cofinal
  let F a := funOfIdeal a our_ideal (cofinal_meets_idealOfCofinals _ to_cofinal (Sum.inl a))
  let G b := invOfIdeal b our_ideal (cofinal_meets_idealOfCofinals _ to_cofinal (Sum.inr b))
  ‚ü®OrderIso.ofCmpEqCmp (fun a ‚Ü¶ (F a).val) (fun b ‚Ü¶ (G b).val) fun a b ‚Ü¶ by
      rcases(F a).prop with ‚ü®f, hf, ha‚ü©
      -- ‚ä¢ cmp a ((fun b => ‚Üë(G b)) b) = cmp ((fun a => ‚Üë(F a)) a) b
      rcases(G b).prop with ‚ü®g, hg, hb‚ü©
      -- ‚ä¢ cmp a ((fun b => ‚Üë(G b)) b) = cmp ((fun a => ‚Üë(F a)) a) b
      rcases our_ideal.directed _ hf _ hg with ‚ü®m, _, fm, gm‚ü©
      -- ‚ä¢ cmp a ((fun b => ‚Üë(G b)) b) = cmp ((fun a => ‚Üë(F a)) a) b
      exact m.prop (a, _) (fm ha) (_, b) (gm hb)‚ü©
      -- üéâ no goals
#align order.iso_of_countable_dense Order.iso_of_countable_dense

end Order
