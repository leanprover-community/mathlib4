/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Data.Set.Pairwise.Basic
import Mathlib.Order.SuccPred.Basic

#align_import order.succ_pred.interval_succ from "leanprover-community/mathlib"@"c227d107bbada5d0d9d20287e3282c0a7f1651a0"

/-!
# Intervals `Ixx (f x) (f (Order.succ x))`

In this file we prove

* `Monotone.biUnion_Ico_Ioc_map_succ`: if `Œ±` is a linear archimedean succ order and `Œ≤` is a linear
  order, then for any monotone function `f` and `m n : Œ±`, the union of intervals
  `Set.Ioc (f i) (f (Order.succ i))`, `m ‚â§ i < n`, is equal to `Set.Ioc (f m) (f n)`;

* `Monotone.pairwise_disjoint_on_Ioc_succ`: if `Œ±` is a linear succ order, `Œ≤` is a preorder, and
  `f : Œ± ‚Üí Œ≤` is a monotone function, then the intervals `Set.Ioc (f n) (f (Order.succ n))` are
  pairwise disjoint.

For the latter lemma, we also prove various order dual versions.
-/


open Set Order

variable {Œ± Œ≤ : Type*} [LinearOrder Œ±]

namespace Monotone

/-- If `Œ±` is a linear archimedean succ order and `Œ≤` is a linear order, then for any monotone
function `f` and `m n : Œ±`, the union of intervals `Set.Ioc (f i) (f (Order.succ i))`, `m ‚â§ i < n`,
is equal to `Set.Ioc (f m) (f n)` -/
theorem biUnion_Ico_Ioc_map_succ [SuccOrder Œ±] [IsSuccArchimedean Œ±] [LinearOrder Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : Monotone f) (m n : Œ±) : ‚ãÉ i ‚àà Ico m n, Ioc (f i) (f (succ i)) = Ioc (f m) (f n) := by
  cases' le_total n m with hnm hmn
  -- ‚ä¢ ‚ãÉ (i : Œ±) (_ : i ‚àà Ico m n), Ioc (f i) (f (succ i)) = Ioc (f m) (f n)
  ¬∑ rw [Ico_eq_empty_of_le hnm, Ioc_eq_empty_of_le (hf hnm), biUnion_empty]
    -- üéâ no goals
  ¬∑ refine' Succ.rec _ _ hmn
    -- ‚ä¢ ‚ãÉ (i : Œ±) (_ : i ‚àà Ico m m), Ioc (f i) (f (succ i)) = Ioc (f m) (f m)
    ¬∑ simp only [Ioc_self, Ico_self, biUnion_empty]
      -- üéâ no goals
    ¬∑ intro k hmk ihk
      -- ‚ä¢ ‚ãÉ (i : Œ±) (_ : i ‚àà Ico m (succ k)), Ioc (f i) (f (succ i)) = Ioc (f m) (f (s ‚Ä¶
      rw [‚Üê Ioc_union_Ioc_eq_Ioc (hf hmk) (hf <| le_succ _), union_comm, ‚Üê ihk]
      -- ‚ä¢ ‚ãÉ (i : Œ±) (_ : i ‚àà Ico m (succ k)), Ioc (f i) (f (succ i)) = Ioc (f k) (f (s ‚Ä¶
      by_cases hk : IsMax k
      -- ‚ä¢ ‚ãÉ (i : Œ±) (_ : i ‚àà Ico m (succ k)), Ioc (f i) (f (succ i)) = Ioc (f k) (f (s ‚Ä¶
      ¬∑ rw [hk.succ_eq, Ioc_self, empty_union]
        -- üéâ no goals
      ¬∑ rw [Ico_succ_right_eq_insert_of_not_isMax hmk hk, biUnion_insert]
        -- üéâ no goals
#align monotone.bUnion_Ico_Ioc_map_succ Monotone.biUnion_Ico_Ioc_map_succ

/-- If `Œ±` is a linear succ order, `Œ≤` is a preorder, and `f : Œ± ‚Üí Œ≤` is a monotone function, then
the intervals `Set.Ioc (f n) (f (Order.succ n))` are pairwise disjoint. -/
theorem pairwise_disjoint_on_Ioc_succ [SuccOrder Œ±] [Preorder Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Monotone f) :
    Pairwise (Disjoint on fun n => Ioc (f n) (f (succ n))) :=
  (pairwise_disjoint_on _).2 fun _ _ hmn =>
    disjoint_iff_inf_le.mpr fun _ ‚ü®‚ü®_, h‚ÇÅ‚ü©, ‚ü®h‚ÇÇ, _‚ü©‚ü© =>
      h‚ÇÇ.not_le <| h‚ÇÅ.trans <| hf <| succ_le_of_lt hmn
#align monotone.pairwise_disjoint_on_Ioc_succ Monotone.pairwise_disjoint_on_Ioc_succ

/-- If `Œ±` is a linear succ order, `Œ≤` is a preorder, and `f : Œ± ‚Üí Œ≤` is a monotone function, then
the intervals `Set.Ico (f n) (f (Order.succ n))` are pairwise disjoint. -/
theorem pairwise_disjoint_on_Ico_succ [SuccOrder Œ±] [Preorder Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Monotone f) :
    Pairwise (Disjoint on fun n => Ico (f n) (f (succ n))) :=
  (pairwise_disjoint_on _).2 fun _ _ hmn =>
    disjoint_iff_inf_le.mpr fun _ ‚ü®‚ü®_, h‚ÇÅ‚ü©, ‚ü®h‚ÇÇ, _‚ü©‚ü© =>
      h‚ÇÅ.not_le <| (hf <| succ_le_of_lt hmn).trans h‚ÇÇ
#align monotone.pairwise_disjoint_on_Ico_succ Monotone.pairwise_disjoint_on_Ico_succ

/-- If `Œ±` is a linear succ order, `Œ≤` is a preorder, and `f : Œ± ‚Üí Œ≤` is a monotone function, then
the intervals `Set.Ioo (f n) (f (Order.succ n))` are pairwise disjoint. -/
theorem pairwise_disjoint_on_Ioo_succ [SuccOrder Œ±] [Preorder Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Monotone f) :
    Pairwise (Disjoint on fun n => Ioo (f n) (f (succ n))) :=
  hf.pairwise_disjoint_on_Ico_succ.mono fun _ _ h => h.mono Ioo_subset_Ico_self Ioo_subset_Ico_self
#align monotone.pairwise_disjoint_on_Ioo_succ Monotone.pairwise_disjoint_on_Ioo_succ

/-- If `Œ±` is a linear pred order, `Œ≤` is a preorder, and `f : Œ± ‚Üí Œ≤` is a monotone function, then
the intervals `Set.Ioc (f Order.pred n) (f n)` are pairwise disjoint. -/
theorem pairwise_disjoint_on_Ioc_pred [PredOrder Œ±] [Preorder Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Monotone f) :
    Pairwise (Disjoint on fun n => Ioc (f (pred n)) (f n)) := by
  simpa only [(¬∑ ‚àò ¬∑), dual_Ico] using hf.dual.pairwise_disjoint_on_Ico_succ
  -- üéâ no goals
#align monotone.pairwise_disjoint_on_Ioc_pred Monotone.pairwise_disjoint_on_Ioc_pred

/-- If `Œ±` is a linear pred order, `Œ≤` is a preorder, and `f : Œ± ‚Üí Œ≤` is a monotone function, then
the intervals `Set.Ico (f Order.pred n) (f n)` are pairwise disjoint. -/
theorem pairwise_disjoint_on_Ico_pred [PredOrder Œ±] [Preorder Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Monotone f) :
    Pairwise (Disjoint on fun n => Ico (f (pred n)) (f n)) := by
  simpa only [(¬∑ ‚àò ¬∑), dual_Ioc] using hf.dual.pairwise_disjoint_on_Ioc_succ
  -- üéâ no goals
#align monotone.pairwise_disjoint_on_Ico_pred Monotone.pairwise_disjoint_on_Ico_pred

/-- If `Œ±` is a linear pred order, `Œ≤` is a preorder, and `f : Œ± ‚Üí Œ≤` is a monotone function, then
the intervals `Set.Ioo (f Order.pred n) (f n)` are pairwise disjoint. -/
theorem pairwise_disjoint_on_Ioo_pred [PredOrder Œ±] [Preorder Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Monotone f) :
    Pairwise (Disjoint on fun n => Ioo (f (pred n)) (f n)) := by
  simpa only [(¬∑ ‚àò ¬∑), dual_Ioo] using hf.dual.pairwise_disjoint_on_Ioo_succ
  -- üéâ no goals
#align monotone.pairwise_disjoint_on_Ioo_pred Monotone.pairwise_disjoint_on_Ioo_pred

end Monotone

namespace Antitone

/-- If `Œ±` is a linear succ order, `Œ≤` is a preorder, and `f : Œ± ‚Üí Œ≤` is an antitone function, then
the intervals `Set.Ioc (f (Order.succ n)) (f n)` are pairwise disjoint. -/
theorem pairwise_disjoint_on_Ioc_succ [SuccOrder Œ±] [Preorder Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Antitone f) :
    Pairwise (Disjoint on fun n => Ioc (f (succ n)) (f n)) :=
  hf.dual_left.pairwise_disjoint_on_Ioc_pred
#align antitone.pairwise_disjoint_on_Ioc_succ Antitone.pairwise_disjoint_on_Ioc_succ

/-- If `Œ±` is a linear succ order, `Œ≤` is a preorder, and `f : Œ± ‚Üí Œ≤` is an antitone function, then
the intervals `Set.Ico (f (Order.succ n)) (f n)` are pairwise disjoint. -/
theorem pairwise_disjoint_on_Ico_succ [SuccOrder Œ±] [Preorder Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Antitone f) :
    Pairwise (Disjoint on fun n => Ico (f (succ n)) (f n)) :=
  hf.dual_left.pairwise_disjoint_on_Ico_pred
#align antitone.pairwise_disjoint_on_Ico_succ Antitone.pairwise_disjoint_on_Ico_succ

/-- If `Œ±` is a linear succ order, `Œ≤` is a preorder, and `f : Œ± ‚Üí Œ≤` is an antitone function, then
the intervals `Set.Ioo (f (Order.succ n)) (f n)` are pairwise disjoint. -/
theorem pairwise_disjoint_on_Ioo_succ [SuccOrder Œ±] [Preorder Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Antitone f) :
    Pairwise (Disjoint on fun n => Ioo (f (succ n)) (f n)) :=
  hf.dual_left.pairwise_disjoint_on_Ioo_pred
#align antitone.pairwise_disjoint_on_Ioo_succ Antitone.pairwise_disjoint_on_Ioo_succ

/-- If `Œ±` is a linear pred order, `Œ≤` is a preorder, and `f : Œ± ‚Üí Œ≤` is an antitone function, then
the intervals `Set.Ioc (f n) (f (Order.pred n))` are pairwise disjoint. -/
theorem pairwise_disjoint_on_Ioc_pred [PredOrder Œ±] [Preorder Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Antitone f) :
    Pairwise (Disjoint on fun n => Ioc (f n) (f (pred n))) :=
  hf.dual_left.pairwise_disjoint_on_Ioc_succ
#align antitone.pairwise_disjoint_on_Ioc_pred Antitone.pairwise_disjoint_on_Ioc_pred

/-- If `Œ±` is a linear pred order, `Œ≤` is a preorder, and `f : Œ± ‚Üí Œ≤` is an antitone function, then
the intervals `Set.Ico (f n) (f (Order.pred n))` are pairwise disjoint. -/
theorem pairwise_disjoint_on_Ico_pred [PredOrder Œ±] [Preorder Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Antitone f) :
    Pairwise (Disjoint on fun n => Ico (f n) (f (pred n))) :=
  hf.dual_left.pairwise_disjoint_on_Ico_succ
#align antitone.pairwise_disjoint_on_Ico_pred Antitone.pairwise_disjoint_on_Ico_pred

/-- If `Œ±` is a linear pred order, `Œ≤` is a preorder, and `f : Œ± ‚Üí Œ≤` is an antitone function, then
the intervals `Set.Ioo (f n) (f (Order.pred n))` are pairwise disjoint. -/
theorem pairwise_disjoint_on_Ioo_pred [PredOrder Œ±] [Preorder Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Antitone f) :
    Pairwise (Disjoint on fun n => Ioo (f n) (f (pred n))) :=
  hf.dual_left.pairwise_disjoint_on_Ioo_succ
#align antitone.pairwise_disjoint_on_Ioo_pred Antitone.pairwise_disjoint_on_Ioo_pred

end Antitone
