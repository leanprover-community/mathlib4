/-
Copyright (c) 2021 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Violeta Hern√°ndez Palacios, Grayson Burton, Floris van Doorn
-/
import Mathlib.Data.Set.Intervals.OrdConnected
import Mathlib.Order.Antisymmetrization

#align_import order.cover from "leanprover-community/mathlib"@"207cfac9fcd06138865b5d04f7091e46d9320432"

/-!
# The covering relation

This file defines the covering relation in an order. `b` is said to cover `a` if `a < b` and there
is no element in between. We say that `b` weakly covers `a` if `a ‚â§ b` and there is no element
between `a` and `b`. In a partial order this is equivalent to `a ‚ãñ b ‚à® a = b`, in a preorder this
is equivalent to `a ‚ãñ b ‚à® (a ‚â§ b ‚àß b ‚â§ a)`

## Notation

* `a ‚ãñ b` means that `b` covers `a`.
* `a ‚©ø b` means that `b` weakly covers `a`.
-/


open Set OrderDual

variable {Œ± Œ≤ : Type*}

section WeaklyCovers

section Preorder

variable [Preorder Œ±] [Preorder Œ≤] {a b c : Œ±}

/-- `Wcovby a b` means that `a = b` or `b` covers `a`.
This means that `a ‚â§ b` and there is no element in between.
-/
def Wcovby (a b : Œ±) : Prop :=
  a ‚â§ b ‚àß ‚àÄ ‚¶Éc‚¶Ñ, a < c ‚Üí ¬¨c < b
#align wcovby Wcovby

/-- Notation for `Wcovby a b`. -/
infixl:50 " ‚©ø " => Wcovby

theorem Wcovby.le (h : a ‚©ø b) : a ‚â§ b :=
  h.1
#align wcovby.le Wcovby.le

theorem Wcovby.refl (a : Œ±) : a ‚©ø a :=
  ‚ü®le_rfl, fun _ hc => hc.not_lt‚ü©
#align wcovby.refl Wcovby.refl

theorem Wcovby.rfl : a ‚©ø a :=
  Wcovby.refl a
#align wcovby.rfl Wcovby.rfl

protected theorem Eq.wcovby (h : a = b) : a ‚©ø b :=
  h ‚ñ∏ Wcovby.rfl
#align eq.wcovby Eq.wcovby

theorem wcovby_of_le_of_le (h1 : a ‚â§ b) (h2 : b ‚â§ a) : a ‚©ø b :=
  ‚ü®h1, fun _ hac hcb => (hac.trans hcb).not_le h2‚ü©
#align wcovby_of_le_of_le wcovby_of_le_of_le

alias LE.le.wcovby_of_le := wcovby_of_le_of_le

theorem AntisymmRel.wcovby (h : AntisymmRel (¬∑ ‚â§ ¬∑) a b) : a ‚©ø b :=
  wcovby_of_le_of_le h.1 h.2
#align antisymm_rel.wcovby AntisymmRel.wcovby

theorem Wcovby.wcovby_iff_le (hab : a ‚©ø b) : b ‚©ø a ‚Üî b ‚â§ a :=
  ‚ü®fun h => h.le, fun h => h.wcovby_of_le hab.le‚ü©
#align wcovby.wcovby_iff_le Wcovby.wcovby_iff_le

theorem wcovby_of_eq_or_eq (hab : a ‚â§ b) (h : ‚àÄ c, a ‚â§ c ‚Üí c ‚â§ b ‚Üí c = a ‚à® c = b) : a ‚©ø b :=
  ‚ü®hab, fun c ha hb => (h c ha.le hb.le).elim ha.ne' hb.ne‚ü©
#align wcovby_of_eq_or_eq wcovby_of_eq_or_eq

theorem AntisymmRel.trans_wcovby (hab : AntisymmRel (¬∑ ‚â§ ¬∑) a b) (hbc : b ‚©ø c) : a ‚©ø c :=
  ‚ü®hab.1.trans hbc.le, fun _ had hdc => hbc.2 (hab.2.trans_lt had) hdc‚ü©
#align antisymm_rel.trans_wcovby AntisymmRel.trans_wcovby

theorem wcovby_congr_left (hab : AntisymmRel (¬∑ ‚â§ ¬∑) a b) : a ‚©ø c ‚Üî b ‚©ø c :=
  ‚ü®hab.symm.trans_wcovby, hab.trans_wcovby‚ü©
#align wcovby_congr_left wcovby_congr_left

theorem Wcovby.trans_antisymm_rel (hab : a ‚©ø b) (hbc : AntisymmRel (¬∑ ‚â§ ¬∑) b c) : a ‚©ø c :=
  ‚ü®hab.le.trans hbc.1, fun _ had hdc => hab.2 had <| hdc.trans_le hbc.2‚ü©
#align wcovby.trans_antisymm_rel Wcovby.trans_antisymm_rel

theorem wcovby_congr_right (hab : AntisymmRel (¬∑ ‚â§ ¬∑) a b) : c ‚©ø a ‚Üî c ‚©ø b :=
  ‚ü®fun h => h.trans_antisymm_rel hab, fun h => h.trans_antisymm_rel hab.symm‚ü©
#align wcovby_congr_right wcovby_congr_right

/-- If `a ‚â§ b`, then `b` does not cover `a` iff there's an element in between. -/
theorem not_wcovby_iff (h : a ‚â§ b) : ¬¨a ‚©ø b ‚Üî ‚àÉ c, a < c ‚àß c < b := by
  simp_rw [Wcovby, h, true_and_iff, not_forall, exists_prop, not_not]
  -- üéâ no goals
#align not_wcovby_iff not_wcovby_iff

instance Wcovby.isRefl : IsRefl Œ± (¬∑ ‚©ø ¬∑) :=
  ‚ü®Wcovby.refl‚ü©
#align wcovby.is_refl Wcovby.isRefl

theorem Wcovby.Ioo_eq (h : a ‚©ø b) : Ioo a b = ‚àÖ :=
  eq_empty_iff_forall_not_mem.2 fun _ hx => h.2 hx.1 hx.2
#align wcovby.Ioo_eq Wcovby.Ioo_eq

theorem wcovby_iff_Ioo_eq : a ‚©ø b ‚Üî a ‚â§ b ‚àß Ioo a b = ‚àÖ :=
  and_congr_right' <| by simp [eq_empty_iff_forall_not_mem]
                         -- üéâ no goals
#align wcovby_iff_Ioo_eq wcovby_iff_Ioo_eq

lemma Wcovby.of_le_of_le (hac : a ‚©ø c) (hab : a ‚â§ b) (hbc : b ‚â§ c) : b ‚©ø c :=
  ‚ü®hbc, fun _x hbx hxc ‚Ü¶ hac.2 (hab.trans_lt hbx) hxc‚ü©

lemma Wcovby.of_le_of_le' (hac : a ‚©ø c) (hab : a ‚â§ b) (hbc : b ‚â§ c) : a ‚©ø b :=
  ‚ü®hab, fun _x hax hxb ‚Ü¶ hac.2 hax <| hxb.trans_le hbc‚ü©

theorem Wcovby.of_image (f : Œ± ‚Ü™o Œ≤) (h : f a ‚©ø f b) : a ‚©ø b :=
  ‚ü®f.le_iff_le.mp h.le, fun _ hac hcb => h.2 (f.lt_iff_lt.mpr hac) (f.lt_iff_lt.mpr hcb)‚ü©
#align wcovby.of_image Wcovby.of_image

theorem Wcovby.image (f : Œ± ‚Ü™o Œ≤) (hab : a ‚©ø b) (h : (range f).OrdConnected) : f a ‚©ø f b := by
  refine' ‚ü®f.monotone hab.le, fun c ha hb => _‚ü©
  -- ‚ä¢ False
  obtain ‚ü®c, rfl‚ü© := h.out (mem_range_self _) (mem_range_self _) ‚ü®ha.le, hb.le‚ü©
  -- ‚ä¢ False
  rw [f.lt_iff_lt] at ha hb
  -- ‚ä¢ False
  exact hab.2 ha hb
  -- üéâ no goals
#align wcovby.image Wcovby.image

theorem Set.OrdConnected.apply_wcovby_apply_iff (f : Œ± ‚Ü™o Œ≤) (h : (range f).OrdConnected) :
    f a ‚©ø f b ‚Üî a ‚©ø b :=
  ‚ü®fun h2 => h2.of_image f, fun hab => hab.image f h‚ü©
#align set.ord_connected.apply_wcovby_apply_iff Set.OrdConnected.apply_wcovby_apply_iff

@[simp]
theorem apply_wcovby_apply_iff {E : Type*} [OrderIsoClass E Œ± Œ≤] (e : E) : e a ‚©ø e b ‚Üî a ‚©ø b :=
  (ordConnected_range (e : Œ± ‚âÉo Œ≤)).apply_wcovby_apply_iff ((e : Œ± ‚âÉo Œ≤) : Œ± ‚Ü™o Œ≤)
#align apply_wcovby_apply_iff apply_wcovby_apply_iff

@[simp]
theorem toDual_wcovby_toDual_iff : toDual b ‚©ø toDual a ‚Üî a ‚©ø b :=
  and_congr_right' <| forall_congr' fun _ => forall_swap
#align to_dual_wcovby_to_dual_iff toDual_wcovby_toDual_iff

@[simp]
theorem ofDual_wcovby_ofDual_iff {a b : Œ±·µí·µà} : ofDual a ‚©ø ofDual b ‚Üî b ‚©ø a :=
  and_congr_right' <| forall_congr' fun _ => forall_swap
#align of_dual_wcovby_of_dual_iff ofDual_wcovby_ofDual_iff

alias ‚ü®_, Wcovby.toDual‚ü© := toDual_wcovby_toDual_iff
#align wcovby.to_dual Wcovby.toDual

alias ‚ü®_, Wcovby.ofDual‚ü© := ofDual_wcovby_ofDual_iff
#align wcovby.of_dual Wcovby.ofDual

end Preorder

section PartialOrder

variable [PartialOrder Œ±] {a b c : Œ±}

theorem Wcovby.eq_or_eq (h : a ‚©ø b) (h2 : a ‚â§ c) (h3 : c ‚â§ b) : c = a ‚à® c = b := by
  rcases h2.eq_or_lt with (h2 | h2); ¬∑ exact Or.inl h2.symm
  -- ‚ä¢ c = a ‚à® c = b
                                       -- üéâ no goals
  rcases h3.eq_or_lt with (h3 | h3); ¬∑ exact Or.inr h3
  -- ‚ä¢ c = a ‚à® c = b
                                       -- üéâ no goals
  exact (h.2 h2 h3).elim
  -- üéâ no goals
#align wcovby.eq_or_eq Wcovby.eq_or_eq

/-- An `iff` version of `Wcovby.eq_or_eq` and `wcovby_of_eq_or_eq`. -/
theorem wcovby_iff_le_and_eq_or_eq : a ‚©ø b ‚Üî a ‚â§ b ‚àß ‚àÄ c, a ‚â§ c ‚Üí c ‚â§ b ‚Üí c = a ‚à® c = b :=
  ‚ü®fun h => ‚ü®h.le, fun _ => h.eq_or_eq‚ü©, And.rec wcovby_of_eq_or_eq‚ü©
#align wcovby_iff_le_and_eq_or_eq wcovby_iff_le_and_eq_or_eq

theorem Wcovby.le_and_le_iff (h : a ‚©ø b) : a ‚â§ c ‚àß c ‚â§ b ‚Üî c = a ‚à® c = b := by
  refine' ‚ü®fun h2 => h.eq_or_eq h2.1 h2.2, _‚ü©; rintro (rfl | rfl);
  -- ‚ä¢ c = a ‚à® c = b ‚Üí a ‚â§ c ‚àß c ‚â§ b
                                               -- ‚ä¢ c ‚â§ c ‚àß c ‚â§ b
  exacts [‚ü®le_rfl, h.le‚ü©, ‚ü®h.le, le_rfl‚ü©]
  -- üéâ no goals
#align wcovby.le_and_le_iff Wcovby.le_and_le_iff

theorem Wcovby.Icc_eq (h : a ‚©ø b) : Icc a b = {a, b} := by
  ext c
  -- ‚ä¢ c ‚àà Icc a b ‚Üî c ‚àà {a, b}
  exact h.le_and_le_iff
  -- üéâ no goals
#align wcovby.Icc_eq Wcovby.Icc_eq

theorem Wcovby.Ico_subset (h : a ‚©ø b) : Ico a b ‚äÜ {a} := by
  rw [‚Üê Icc_diff_right, h.Icc_eq, diff_singleton_subset_iff, pair_comm]
  -- üéâ no goals
#align wcovby.Ico_subset Wcovby.Ico_subset

theorem Wcovby.Ioc_subset (h : a ‚©ø b) : Ioc a b ‚äÜ {b} := by
  rw [‚Üê Icc_diff_left, h.Icc_eq, diff_singleton_subset_iff]
  -- üéâ no goals
#align wcovby.Ioc_subset Wcovby.Ioc_subset

end PartialOrder

section SemilatticeSup

variable [SemilatticeSup Œ±] {a b c : Œ±}

theorem Wcovby.sup_eq (hac : a ‚©ø c) (hbc : b ‚©ø c) (hab : a ‚â† b) : a ‚äî b = c :=
  (sup_le hac.le hbc.le).eq_of_not_lt fun h =>
    hab.lt_sup_or_lt_sup.elim (fun h' => hac.2 h' h) fun h' => hbc.2 h' h
#align wcovby.sup_eq Wcovby.sup_eq

end SemilatticeSup

section SemilatticeInf

variable [SemilatticeInf Œ±] {a b c : Œ±}

theorem Wcovby.inf_eq (hca : c ‚©ø a) (hcb : c ‚©ø b) (hab : a ‚â† b) : a ‚äì b = c :=
  (le_inf hca.le hcb.le).eq_of_not_gt fun h => hab.inf_lt_or_inf_lt.elim (hca.2 h) (hcb.2 h)
#align wcovby.inf_eq Wcovby.inf_eq

end SemilatticeInf

end WeaklyCovers

section LT

variable [LT Œ±] {a b : Œ±}

/-- `Covby a b` means that `b` covers `a`: `a < b` and there is no element in between. -/
def Covby (a b : Œ±) : Prop :=
  a < b ‚àß ‚àÄ ‚¶Éc‚¶Ñ, a < c ‚Üí ¬¨c < b
#align covby Covby

/-- Notation for `Covby a b`. -/
infixl:50 " ‚ãñ " => Covby

theorem Covby.lt (h : a ‚ãñ b) : a < b :=
  h.1
#align covby.lt Covby.lt

/-- If `a < b`, then `b` does not cover `a` iff there's an element in between. -/
theorem not_covby_iff (h : a < b) : ¬¨a ‚ãñ b ‚Üî ‚àÉ c, a < c ‚àß c < b := by
  simp_rw [Covby, h, true_and_iff, not_forall, exists_prop, not_not]
  -- üéâ no goals
#align not_covby_iff not_covby_iff

alias ‚ü®exists_lt_lt_of_not_covby, _‚ü© := not_covby_iff
#align exists_lt_lt_of_not_covby exists_lt_lt_of_not_covby

alias LT.lt.exists_lt_lt := exists_lt_lt_of_not_covby

/-- In a dense order, nothing covers anything. -/
theorem not_covby [DenselyOrdered Œ±] : ¬¨a ‚ãñ b := fun h =>
  let ‚ü®_, hc‚ü© := exists_between h.1
  h.2 hc.1 hc.2
#align not_covby not_covby

theorem densely_ordered_iff_forall_not_covby : DenselyOrdered Œ± ‚Üî ‚àÄ a b : Œ±, ¬¨a ‚ãñ b :=
  ‚ü®fun h _ _ => @not_covby _ _ _ _ h, fun h =>
    ‚ü®fun _ _ hab => exists_lt_lt_of_not_covby hab <| h _ _‚ü©‚ü©
#align densely_ordered_iff_forall_not_covby densely_ordered_iff_forall_not_covby

@[simp]
theorem toDual_covby_toDual_iff : toDual b ‚ãñ toDual a ‚Üî a ‚ãñ b :=
  and_congr_right' <| forall_congr' fun _ => forall_swap
#align to_dual_covby_to_dual_iff toDual_covby_toDual_iff

@[simp]
theorem ofDual_covby_ofDual_iff {a b : Œ±·µí·µà} : ofDual a ‚ãñ ofDual b ‚Üî b ‚ãñ a :=
  and_congr_right' <| forall_congr' fun _ => forall_swap
#align of_dual_covby_of_dual_iff ofDual_covby_ofDual_iff

alias ‚ü®_, Covby.toDual‚ü© := toDual_covby_toDual_iff
#align covby.to_dual Covby.toDual

alias ‚ü®_, Covby.ofDual‚ü© := ofDual_covby_ofDual_iff
#align covby.of_dual Covby.ofDual

end LT

section Preorder

variable [Preorder Œ±] [Preorder Œ≤] {a b c : Œ±}

theorem Covby.le (h : a ‚ãñ b) : a ‚â§ b :=
  h.1.le
#align covby.le Covby.le

protected theorem Covby.ne (h : a ‚ãñ b) : a ‚â† b :=
  h.lt.ne
#align covby.ne Covby.ne

theorem Covby.ne' (h : a ‚ãñ b) : b ‚â† a :=
  h.lt.ne'
#align covby.ne' Covby.ne'

protected theorem Covby.wcovby (h : a ‚ãñ b) : a ‚©ø b :=
  ‚ü®h.le, h.2‚ü©
#align covby.wcovby Covby.wcovby

theorem Wcovby.covby_of_not_le (h : a ‚©ø b) (h2 : ¬¨b ‚â§ a) : a ‚ãñ b :=
  ‚ü®h.le.lt_of_not_le h2, h.2‚ü©
#align wcovby.covby_of_not_le Wcovby.covby_of_not_le

theorem Wcovby.covby_of_lt (h : a ‚©ø b) (h2 : a < b) : a ‚ãñ b :=
  ‚ü®h2, h.2‚ü©
#align wcovby.covby_of_lt Wcovby.covby_of_lt

lemma Covby.of_le_of_lt (hac : a ‚ãñ c) (hab : a ‚â§ b) (hbc : b < c) : b ‚ãñ c :=
  ‚ü®hbc, fun _x hbx hxc ‚Ü¶ hac.2 (hab.trans_lt hbx) hxc‚ü©

lemma Covby.of_lt_of_le (hac : a ‚ãñ c) (hab : a < b) (hbc : b ‚â§ c) : a ‚ãñ b :=
  ‚ü®hab, fun _x hax hxb ‚Ü¶ hac.2 hax <| hxb.trans_le hbc‚ü©

theorem not_covby_of_lt_of_lt (h‚ÇÅ : a < b) (h‚ÇÇ : b < c) : ¬¨a ‚ãñ c :=
  (not_covby_iff (h‚ÇÅ.trans h‚ÇÇ)).2 ‚ü®b, h‚ÇÅ, h‚ÇÇ‚ü©
#align not_covby_of_lt_of_lt not_covby_of_lt_of_lt

theorem covby_iff_wcovby_and_lt : a ‚ãñ b ‚Üî a ‚©ø b ‚àß a < b :=
  ‚ü®fun h => ‚ü®h.wcovby, h.lt‚ü©, fun h => h.1.covby_of_lt h.2‚ü©
#align covby_iff_wcovby_and_lt covby_iff_wcovby_and_lt

theorem covby_iff_wcovby_and_not_le : a ‚ãñ b ‚Üî a ‚©ø b ‚àß ¬¨b ‚â§ a :=
  ‚ü®fun h => ‚ü®h.wcovby, h.lt.not_le‚ü©, fun h => h.1.covby_of_not_le h.2‚ü©
#align covby_iff_wcovby_and_not_le covby_iff_wcovby_and_not_le

theorem wcovby_iff_covby_or_le_and_le : a ‚©ø b ‚Üî a ‚ãñ b ‚à® a ‚â§ b ‚àß b ‚â§ a :=
  ‚ü®fun h => or_iff_not_imp_right.mpr fun h' => h.covby_of_not_le fun hba => h' ‚ü®h.le, hba‚ü©,
    fun h' => h'.elim (fun h => h.wcovby) fun h => h.1.wcovby_of_le h.2‚ü©
#align wcovby_iff_covby_or_le_and_le wcovby_iff_covby_or_le_and_le

theorem AntisymmRel.trans_covby (hab : AntisymmRel (¬∑ ‚â§ ¬∑) a b) (hbc : b ‚ãñ c) : a ‚ãñ c :=
  ‚ü®hab.1.trans_lt hbc.lt, fun _ had hdc => hbc.2 (hab.2.trans_lt had) hdc‚ü©
#align antisymm_rel.trans_covby AntisymmRel.trans_covby

theorem covby_congr_left (hab : AntisymmRel (¬∑ ‚â§ ¬∑) a b) : a ‚ãñ c ‚Üî b ‚ãñ c :=
  ‚ü®hab.symm.trans_covby, hab.trans_covby‚ü©
#align covby_congr_left covby_congr_left

theorem Covby.trans_antisymmRel (hab : a ‚ãñ b) (hbc : AntisymmRel (¬∑ ‚â§ ¬∑) b c) : a ‚ãñ c :=
  ‚ü®hab.lt.trans_le hbc.1, fun _ had hdb => hab.2 had <| hdb.trans_le hbc.2‚ü©
#align covby.trans_antisymm_rel Covby.trans_antisymmRel

theorem covby_congr_right (hab : AntisymmRel (¬∑ ‚â§ ¬∑) a b) : c ‚ãñ a ‚Üî c ‚ãñ b :=
  ‚ü®fun h => h.trans_antisymmRel hab, fun h => h.trans_antisymmRel hab.symm‚ü©
#align covby_congr_right covby_congr_right

instance : IsNonstrictStrictOrder Œ± (¬∑ ‚©ø ¬∑) (¬∑ ‚ãñ ¬∑) :=
  ‚ü®fun _ _ =>
    covby_iff_wcovby_and_not_le.trans <| and_congr_right fun h => h.wcovby_iff_le.not.symm‚ü©

instance Covby.isIrrefl : IsIrrefl Œ± (¬∑ ‚ãñ ¬∑) :=
  ‚ü®fun _ ha => ha.ne rfl‚ü©
#align covby.is_irrefl Covby.isIrrefl

theorem Covby.Ioo_eq (h : a ‚ãñ b) : Ioo a b = ‚àÖ :=
  h.wcovby.Ioo_eq
#align covby.Ioo_eq Covby.Ioo_eq

theorem covby_iff_Ioo_eq : a ‚ãñ b ‚Üî a < b ‚àß Ioo a b = ‚àÖ :=
  and_congr_right' <| by simp [eq_empty_iff_forall_not_mem]
                         -- üéâ no goals
#align covby_iff_Ioo_eq covby_iff_Ioo_eq

theorem Covby.of_image (f : Œ± ‚Ü™o Œ≤) (h : f a ‚ãñ f b) : a ‚ãñ b :=
  ‚ü®f.lt_iff_lt.mp h.lt, fun _ hac hcb => h.2 (f.lt_iff_lt.mpr hac) (f.lt_iff_lt.mpr hcb)‚ü©
#align covby.of_image Covby.of_image

theorem Covby.image (f : Œ± ‚Ü™o Œ≤) (hab : a ‚ãñ b) (h : (range f).OrdConnected) : f a ‚ãñ f b :=
  (hab.wcovby.image f h).covby_of_lt <| f.strictMono hab.lt
#align covby.image Covby.image

theorem Set.OrdConnected.apply_covby_apply_iff (f : Œ± ‚Ü™o Œ≤) (h : (range f).OrdConnected) :
    f a ‚ãñ f b ‚Üî a ‚ãñ b :=
  ‚ü®Covby.of_image f, fun hab => hab.image f h‚ü©
#align set.ord_connected.apply_covby_apply_iff Set.OrdConnected.apply_covby_apply_iff

@[simp]
theorem apply_covby_apply_iff {E : Type*} [OrderIsoClass E Œ± Œ≤] (e : E) : e a ‚ãñ e b ‚Üî a ‚ãñ b :=
  (ordConnected_range (e : Œ± ‚âÉo Œ≤)).apply_covby_apply_iff ((e : Œ± ‚âÉo Œ≤) : Œ± ‚Ü™o Œ≤)
#align apply_covby_apply_iff apply_covby_apply_iff

theorem covby_of_eq_or_eq (hab : a < b) (h : ‚àÄ c, a ‚â§ c ‚Üí c ‚â§ b ‚Üí c = a ‚à® c = b) : a ‚ãñ b :=
  ‚ü®hab, fun c ha hb => (h c ha.le hb.le).elim ha.ne' hb.ne‚ü©
#align covby_of_eq_or_eq covby_of_eq_or_eq

end Preorder

section PartialOrder

variable [PartialOrder Œ±] {a b c : Œ±}

theorem Wcovby.covby_of_ne (h : a ‚©ø b) (h2 : a ‚â† b) : a ‚ãñ b :=
  ‚ü®h.le.lt_of_ne h2, h.2‚ü©
#align wcovby.covby_of_ne Wcovby.covby_of_ne

theorem covby_iff_wcovby_and_ne : a ‚ãñ b ‚Üî a ‚©ø b ‚àß a ‚â† b :=
  ‚ü®fun h => ‚ü®h.wcovby, h.ne‚ü©, fun h => h.1.covby_of_ne h.2‚ü©
#align covby_iff_wcovby_and_ne covby_iff_wcovby_and_ne

theorem wcovby_iff_covby_or_eq : a ‚©ø b ‚Üî a ‚ãñ b ‚à® a = b := by
  rw [le_antisymm_iff, wcovby_iff_covby_or_le_and_le]
  -- üéâ no goals
#align wcovby_iff_covby_or_eq wcovby_iff_covby_or_eq

theorem wcovby_iff_eq_or_covby : a ‚©ø b ‚Üî a = b ‚à® a ‚ãñ b :=
  wcovby_iff_covby_or_eq.trans or_comm
#align wcovby_iff_eq_or_covby wcovby_iff_eq_or_covby

alias ‚ü®Wcovby.covby_or_eq, _‚ü© := wcovby_iff_covby_or_eq
#align wcovby.covby_or_eq Wcovby.covby_or_eq

alias ‚ü®Wcovby.eq_or_covby, _‚ü© := wcovby_iff_eq_or_covby
#align wcovby.eq_or_covby Wcovby.eq_or_covby

theorem Covby.eq_or_eq (h : a ‚ãñ b) (h2 : a ‚â§ c) (h3 : c ‚â§ b) : c = a ‚à® c = b :=
  h.wcovby.eq_or_eq h2 h3
#align covby.eq_or_eq Covby.eq_or_eq

/-- An `iff` version of `Covby.eq_or_eq` and `covby_of_eq_or_eq`. -/
theorem covby_iff_lt_and_eq_or_eq : a ‚ãñ b ‚Üî a < b ‚àß ‚àÄ c, a ‚â§ c ‚Üí c ‚â§ b ‚Üí c = a ‚à® c = b :=
  ‚ü®fun h => ‚ü®h.lt, fun _ => h.eq_or_eq‚ü©, And.rec covby_of_eq_or_eq‚ü©
#align covby_iff_lt_and_eq_or_eq covby_iff_lt_and_eq_or_eq

theorem Covby.Ico_eq (h : a ‚ãñ b) : Ico a b = {a} := by
  rw [‚Üê Ioo_union_left h.lt, h.Ioo_eq, empty_union]
  -- üéâ no goals
#align covby.Ico_eq Covby.Ico_eq

theorem Covby.Ioc_eq (h : a ‚ãñ b) : Ioc a b = {b} := by
  rw [‚Üê Ioo_union_right h.lt, h.Ioo_eq, empty_union]
  -- üéâ no goals
#align covby.Ioc_eq Covby.Ioc_eq

theorem Covby.Icc_eq (h : a ‚ãñ b) : Icc a b = {a, b} :=
  h.wcovby.Icc_eq
#align covby.Icc_eq Covby.Icc_eq

end PartialOrder

section LinearOrder

variable [LinearOrder Œ±] {a b c : Œ±}

theorem Covby.Ioi_eq (h : a ‚ãñ b) : Ioi a = Ici b := by
  rw [‚Üê Ioo_union_Ici_eq_Ioi h.lt, h.Ioo_eq, empty_union]
  -- üéâ no goals
#align covby.Ioi_eq Covby.Ioi_eq

theorem Covby.Iio_eq (h : a ‚ãñ b) : Iio b = Iic a := by
  rw [‚Üê Iic_union_Ioo_eq_Iio h.lt, h.Ioo_eq, union_empty]
  -- üéâ no goals
#align covby.Iio_eq Covby.Iio_eq

theorem Wcovby.le_of_lt (hab : a ‚©ø b) (hcb : c < b) : c ‚â§ a :=
  not_lt.1 fun hac => hab.2 hac hcb
#align wcovby.le_of_lt Wcovby.le_of_lt

theorem Wcovby.ge_of_gt (hab : a ‚©ø b) (hac : a < c) : b ‚â§ c :=
  not_lt.1 <| hab.2 hac
#align wcovby.ge_of_gt Wcovby.ge_of_gt

theorem Covby.le_of_lt (hab : a ‚ãñ b) : c < b ‚Üí c ‚â§ a :=
  hab.wcovby.le_of_lt
#align covby.le_of_lt Covby.le_of_lt

theorem Covby.ge_of_gt (hab : a ‚ãñ b) : a < c ‚Üí b ‚â§ c :=
  hab.wcovby.ge_of_gt
#align covby.ge_of_gt Covby.ge_of_gt

theorem Covby.unique_left (ha : a ‚ãñ c) (hb : b ‚ãñ c) : a = b :=
  (hb.le_of_lt ha.lt).antisymm <| ha.le_of_lt hb.lt
#align covby.unique_left Covby.unique_left

theorem Covby.unique_right (hb : a ‚ãñ b) (hc : a ‚ãñ c) : b = c :=
  (hb.ge_of_gt hc.lt).antisymm <| hc.ge_of_gt hb.lt
#align covby.unique_right Covby.unique_right

/-- If `a`, `b`, `c` are consecutive and `a < x < c` then `x = b`. -/
theorem Covby.eq_of_between {x : Œ±} (hab : a ‚ãñ b) (hbc : b ‚ãñ c) (hax : a < x) (hxc : x < c) :
    x = b :=
  le_antisymm (le_of_not_lt fun h => hbc.2 h hxc) (le_of_not_lt <| hab.2 hax)
#align covby.eq_of_between Covby.eq_of_between

/-- If `a < b` then there exist `a' > a` and `b' < b` such that `Set.Iio a'` is strictly to the left
of `Set.Ioi b'`. -/
lemma LT.lt.exists_disjoint_Iio_Ioi (h : a < b) :
    ‚àÉ a' > a, ‚àÉ b' < b, ‚àÄ x < a', ‚àÄ y > b', x < y := by
  by_cases h' : a ‚ãñ b
  -- ‚ä¢ ‚àÉ a', a' > a ‚àß ‚àÉ b', b' < b ‚àß ‚àÄ (x : Œ±), x < a' ‚Üí ‚àÄ (y : Œ±), y > b' ‚Üí x < y
  ¬∑ exact ‚ü®b, h, a, h, fun x hx y hy => hx.trans_le <| h'.ge_of_gt hy‚ü©
    -- üéâ no goals
  ¬∑ rcases h.exists_lt_lt h' with ‚ü®c, ha, hb‚ü©
    -- ‚ä¢ ‚àÉ a', a' > a ‚àß ‚àÉ b', b' < b ‚àß ‚àÄ (x : Œ±), x < a' ‚Üí ‚àÄ (y : Œ±), y > b' ‚Üí x < y
    exact ‚ü®c, ha, c, hb, fun _ h‚ÇÅ _ => lt_trans h‚ÇÅ‚ü©
    -- üéâ no goals

end LinearOrder

namespace Set

theorem wcovby_insert (x : Œ±) (s : Set Œ±) : s ‚©ø insert x s := by
  refine' wcovby_of_eq_or_eq (subset_insert x s) fun t hst h2t => _
  -- ‚ä¢ t = s ‚à® t = insert x s
  by_cases h : x ‚àà t
  -- ‚ä¢ t = s ‚à® t = insert x s
  ¬∑ exact Or.inr (subset_antisymm h2t <| insert_subset_iff.mpr ‚ü®h, hst‚ü©)
    -- üéâ no goals
  ¬∑ refine' Or.inl (subset_antisymm _ hst)
    -- ‚ä¢ t ‚äÜ s
    rwa [‚Üê diff_singleton_eq_self h, diff_singleton_subset_iff]
    -- üéâ no goals
#align set.wcovby_insert Set.wcovby_insert

theorem covby_insert {x : Œ±} {s : Set Œ±} (hx : x ‚àâ s) : s ‚ãñ insert x s :=
  (wcovby_insert x s).covby_of_lt <| ssubset_insert hx
#align set.covby_insert Set.covby_insert

end Set

section Relation

open Relation

lemma wcovby_eq_reflGen_covby [PartialOrder Œ±] : ((¬∑ : Œ±) ‚©ø ¬∑) = ReflGen (¬∑ ‚ãñ ¬∑) := by
  ext x y; simp_rw [wcovby_iff_eq_or_covby, @eq_comm _ x, reflGen_iff]
  -- ‚ä¢ x ‚©ø y ‚Üî ReflGen (fun x x_1 => x ‚ãñ x_1) x y
           -- üéâ no goals

lemma transGen_wcovby_eq_reflTransGen_covby [PartialOrder Œ±] :
    TransGen ((¬∑ : Œ±) ‚©ø ¬∑) = ReflTransGen (¬∑ ‚ãñ ¬∑) := by
  rw [wcovby_eq_reflGen_covby, transGen_reflGen]
  -- üéâ no goals

lemma reflTransGen_wcovby_eq_reflTransGen_covby [PartialOrder Œ±] :
    ReflTransGen ((¬∑ : Œ±) ‚©ø ¬∑) = ReflTransGen (¬∑ ‚ãñ ¬∑) := by
  rw [wcovby_eq_reflGen_covby, reflTransGen_reflGen]
  -- üéâ no goals

end Relation

namespace Prod

variable [PartialOrder Œ±] [PartialOrder Œ≤] {a a‚ÇÅ a‚ÇÇ : Œ±} {b b‚ÇÅ b‚ÇÇ : Œ≤} {x y : Œ± √ó Œ≤}

@[simp]
theorem swap_wcovby_swap : x.swap ‚©ø y.swap ‚Üî x ‚©ø y :=
  apply_wcovby_apply_iff (OrderIso.prodComm : Œ± √ó Œ≤ ‚âÉo Œ≤ √ó Œ±)
#align prod.swap_wcovby_swap Prod.swap_wcovby_swap

@[simp]
theorem swap_covby_swap : x.swap ‚ãñ y.swap ‚Üî x ‚ãñ y :=
  apply_covby_apply_iff (OrderIso.prodComm : Œ± √ó Œ≤ ‚âÉo Œ≤ √ó Œ±)
#align prod.swap_covby_swap Prod.swap_covby_swap

theorem fst_eq_or_snd_eq_of_wcovby : x ‚©ø y ‚Üí x.1 = y.1 ‚à® x.2 = y.2 := by
  refine' fun h => of_not_not fun hab => _
  -- ‚ä¢ False
  push_neg at hab
  -- ‚ä¢ False
  exact
    h.2 (mk_lt_mk.2 <| Or.inl ‚ü®hab.1.lt_of_le h.1.1, le_rfl‚ü©)
      (mk_lt_mk.2 <| Or.inr ‚ü®le_rfl, hab.2.lt_of_le h.1.2‚ü©)
#align prod.fst_eq_or_snd_eq_of_wcovby Prod.fst_eq_or_snd_eq_of_wcovby

theorem _root_.Wcovby.fst (h : x ‚©ø y) : x.1 ‚©ø y.1 :=
  ‚ü®h.1.1, fun _ h‚ÇÅ h‚ÇÇ => h.2 (mk_lt_mk_iff_left.2 h‚ÇÅ) ‚ü®‚ü®h‚ÇÇ.le, h.1.2‚ü©, fun hc => h‚ÇÇ.not_le hc.1‚ü©‚ü©
#align wcovby.fst Wcovby.fst

theorem _root_.Wcovby.snd (h : x ‚©ø y) : x.2 ‚©ø y.2 :=
  ‚ü®h.1.2, fun _ h‚ÇÅ h‚ÇÇ => h.2 (mk_lt_mk_iff_right.2 h‚ÇÅ) ‚ü®‚ü®h.1.1, h‚ÇÇ.le‚ü©, fun hc => h‚ÇÇ.not_le hc.2‚ü©‚ü©
#align wcovby.snd Wcovby.snd

theorem mk_wcovby_mk_iff_left : (a‚ÇÅ, b) ‚©ø (a‚ÇÇ, b) ‚Üî a‚ÇÅ ‚©ø a‚ÇÇ := by
  refine' ‚ü®Wcovby.fst, (And.imp mk_le_mk_iff_left.2) fun h c h‚ÇÅ h‚ÇÇ => _‚ü©
  -- ‚ä¢ False
  have : c.2 = b := h‚ÇÇ.le.2.antisymm h‚ÇÅ.le.2
  -- ‚ä¢ False
  rw [‚Üê @Prod.mk.eta _ _ c, this, mk_lt_mk_iff_left] at h‚ÇÅ h‚ÇÇ
  -- ‚ä¢ False
  exact h h‚ÇÅ h‚ÇÇ
  -- üéâ no goals
#align prod.mk_wcovby_mk_iff_left Prod.mk_wcovby_mk_iff_left

theorem mk_wcovby_mk_iff_right : (a, b‚ÇÅ) ‚©ø (a, b‚ÇÇ) ‚Üî b‚ÇÅ ‚©ø b‚ÇÇ :=
  swap_wcovby_swap.trans mk_wcovby_mk_iff_left
#align prod.mk_wcovby_mk_iff_right Prod.mk_wcovby_mk_iff_right

theorem mk_covby_mk_iff_left : (a‚ÇÅ, b) ‚ãñ (a‚ÇÇ, b) ‚Üî a‚ÇÅ ‚ãñ a‚ÇÇ := by
  simp_rw [covby_iff_wcovby_and_lt, mk_wcovby_mk_iff_left, mk_lt_mk_iff_left]
  -- üéâ no goals
#align prod.mk_covby_mk_iff_left Prod.mk_covby_mk_iff_left

theorem mk_covby_mk_iff_right : (a, b‚ÇÅ) ‚ãñ (a, b‚ÇÇ) ‚Üî b‚ÇÅ ‚ãñ b‚ÇÇ := by
  simp_rw [covby_iff_wcovby_and_lt, mk_wcovby_mk_iff_right, mk_lt_mk_iff_right]
  -- üéâ no goals
#align prod.mk_covby_mk_iff_right Prod.mk_covby_mk_iff_right

theorem mk_wcovby_mk_iff : (a‚ÇÅ, b‚ÇÅ) ‚©ø (a‚ÇÇ, b‚ÇÇ) ‚Üî a‚ÇÅ ‚©ø a‚ÇÇ ‚àß b‚ÇÅ = b‚ÇÇ ‚à® b‚ÇÅ ‚©ø b‚ÇÇ ‚àß a‚ÇÅ = a‚ÇÇ := by
  refine' ‚ü®fun h => _, _‚ü©
  -- ‚ä¢ a‚ÇÅ ‚©ø a‚ÇÇ ‚àß b‚ÇÅ = b‚ÇÇ ‚à® b‚ÇÅ ‚©ø b‚ÇÇ ‚àß a‚ÇÅ = a‚ÇÇ
  ¬∑ obtain rfl | rfl : a‚ÇÅ = a‚ÇÇ ‚à® b‚ÇÅ = b‚ÇÇ := fst_eq_or_snd_eq_of_wcovby h
    -- ‚ä¢ a‚ÇÅ ‚©ø a‚ÇÅ ‚àß b‚ÇÅ = b‚ÇÇ ‚à® b‚ÇÅ ‚©ø b‚ÇÇ ‚àß a‚ÇÅ = a‚ÇÅ
    ¬∑ exact Or.inr ‚ü®mk_wcovby_mk_iff_right.1 h, rfl‚ü©
      -- üéâ no goals
    ¬∑ exact Or.inl ‚ü®mk_wcovby_mk_iff_left.1 h, rfl‚ü©
      -- üéâ no goals
  ¬∑ rintro (‚ü®h, rfl‚ü© | ‚ü®h, rfl‚ü©)
    -- ‚ä¢ (a‚ÇÅ, b‚ÇÅ) ‚©ø (a‚ÇÇ, b‚ÇÅ)
    ¬∑ exact mk_wcovby_mk_iff_left.2 h
      -- üéâ no goals
    ¬∑ exact mk_wcovby_mk_iff_right.2 h
      -- üéâ no goals
#align prod.mk_wcovby_mk_iff Prod.mk_wcovby_mk_iff

theorem mk_covby_mk_iff : (a‚ÇÅ, b‚ÇÅ) ‚ãñ (a‚ÇÇ, b‚ÇÇ) ‚Üî a‚ÇÅ ‚ãñ a‚ÇÇ ‚àß b‚ÇÅ = b‚ÇÇ ‚à® b‚ÇÅ ‚ãñ b‚ÇÇ ‚àß a‚ÇÅ = a‚ÇÇ := by
  refine' ‚ü®fun h => _, _‚ü©
  -- ‚ä¢ a‚ÇÅ ‚ãñ a‚ÇÇ ‚àß b‚ÇÅ = b‚ÇÇ ‚à® b‚ÇÅ ‚ãñ b‚ÇÇ ‚àß a‚ÇÅ = a‚ÇÇ
  ¬∑ obtain rfl | rfl : a‚ÇÅ = a‚ÇÇ ‚à® b‚ÇÅ = b‚ÇÇ := fst_eq_or_snd_eq_of_wcovby h.wcovby
    -- ‚ä¢ a‚ÇÅ ‚ãñ a‚ÇÅ ‚àß b‚ÇÅ = b‚ÇÇ ‚à® b‚ÇÅ ‚ãñ b‚ÇÇ ‚àß a‚ÇÅ = a‚ÇÅ
    ¬∑ exact Or.inr ‚ü®mk_covby_mk_iff_right.1 h, rfl‚ü©
      -- üéâ no goals
    ¬∑ exact Or.inl ‚ü®mk_covby_mk_iff_left.1 h, rfl‚ü©
      -- üéâ no goals
  ¬∑ rintro (‚ü®h, rfl‚ü© | ‚ü®h, rfl‚ü©)
    -- ‚ä¢ (a‚ÇÅ, b‚ÇÅ) ‚ãñ (a‚ÇÇ, b‚ÇÅ)
    ¬∑ exact mk_covby_mk_iff_left.2 h
      -- üéâ no goals
    ¬∑ exact mk_covby_mk_iff_right.2 h
      -- üéâ no goals
#align prod.mk_covby_mk_iff Prod.mk_covby_mk_iff

theorem wcovby_iff : x ‚©ø y ‚Üî x.1 ‚©ø y.1 ‚àß x.2 = y.2 ‚à® x.2 ‚©ø y.2 ‚àß x.1 = y.1 := by
  cases x
  -- ‚ä¢ (fst‚úù, snd‚úù) ‚©ø y ‚Üî (fst‚úù, snd‚úù).fst ‚©ø y.fst ‚àß (fst‚úù, snd‚úù).snd = y.snd ‚à® (fs ‚Ä¶
  cases y
  -- ‚ä¢ (fst‚úù¬π, snd‚úù¬π) ‚©ø (fst‚úù, snd‚úù) ‚Üî (fst‚úù¬π, snd‚úù¬π).fst ‚©ø (fst‚úù, snd‚úù).fst ‚àß (fst ‚Ä¶
  exact mk_wcovby_mk_iff
  -- üéâ no goals
#align prod.wcovby_iff Prod.wcovby_iff

theorem covby_iff : x ‚ãñ y ‚Üî x.1 ‚ãñ y.1 ‚àß x.2 = y.2 ‚à® x.2 ‚ãñ y.2 ‚àß x.1 = y.1 := by
  cases x
  -- ‚ä¢ (fst‚úù, snd‚úù) ‚ãñ y ‚Üî (fst‚úù, snd‚úù).fst ‚ãñ y.fst ‚àß (fst‚úù, snd‚úù).snd = y.snd ‚à® (fs ‚Ä¶
  cases y
  -- ‚ä¢ (fst‚úù¬π, snd‚úù¬π) ‚ãñ (fst‚úù, snd‚úù) ‚Üî (fst‚úù¬π, snd‚úù¬π).fst ‚ãñ (fst‚úù, snd‚úù).fst ‚àß (fst ‚Ä¶
  exact mk_covby_mk_iff
  -- üéâ no goals
#align prod.covby_iff Prod.covby_iff

end Prod
