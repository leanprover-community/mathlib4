/-
Copyright (c) 2021 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.Order.Atoms
import Mathlib.Order.OrderIsoNat
import Mathlib.Order.RelIso.Set
import Mathlib.Order.SupIndep
import Mathlib.Order.Zorn
import Mathlib.Data.Finset.Order
import Mathlib.Data.Set.Intervals.OrderIso
import Mathlib.Data.Finite.Set
import Mathlib.Tactic.TFAE

#align_import order.compactly_generated from "leanprover-community/mathlib"@"c813ed7de0f5115f956239124e9b30f3a621966f"

/-!
# Compactness properties for complete lattices

For complete lattices, there are numerous equivalent ways to express the fact that the relation `>`
is well-founded. In this file we define three especially-useful characterisations and provide
proofs that they are indeed equivalent to well-foundedness.

## Main definitions
 * `CompleteLattice.IsSupClosedCompact`
 * `CompleteLattice.IsSupFiniteCompact`
 * `CompleteLattice.IsCompactElement`
 * `IsCompactlyGenerated`

## Main results
The main result is that the following four conditions are equivalent for a complete lattice:
 * `well_founded (>)`
 * `CompleteLattice.IsSupClosedCompact`
 * `CompleteLattice.IsSupFiniteCompact`
 * `âˆ€ k, CompleteLattice.IsCompactElement k`

This is demonstrated by means of the following four lemmas:
 * `CompleteLattice.WellFounded.isSupFiniteCompact`
 * `CompleteLattice.IsSupFiniteCompact.isSupClosedCompact`
 * `CompleteLattice.IsSupClosedCompact.wellFounded`
 * `CompleteLattice.isSupFiniteCompact_iff_all_elements_compact`

 We also show well-founded lattices are compactly generated
 (`CompleteLattice.isCompactlyGenerated_of_wellFounded`).

## References
- [G. CÄƒlugÄƒreanu, *Lattice Concepts of Module Theory*][calugareanu]

## Tags

complete lattice, well-founded, compact
-/

variable {Î¹ : Sort*} {Î± : Type*} [CompleteLattice Î±] {f : Î¹ â†’ Î±}

namespace CompleteLattice

variable (Î±)

/-- A compactness property for a complete lattice is that any `sup`-closed non-empty subset
contains its `sSup`. -/
def IsSupClosedCompact : Prop :=
  âˆ€ (s : Set Î±) (_ : s.Nonempty), (âˆ€ (a) (_ : a âˆˆ s) (b) (_ : b âˆˆ s), a âŠ” b âˆˆ s) â†’ sSup s âˆˆ s
#align complete_lattice.is_sup_closed_compact CompleteLattice.IsSupClosedCompact

/-- A compactness property for a complete lattice is that any subset has a finite subset with the
same `sSup`. -/
def IsSupFiniteCompact : Prop :=
  âˆ€ s : Set Î±, âˆƒ t : Finset Î±, â†‘t âŠ† s âˆ§ sSup s = t.sup id
#align complete_lattice.is_Sup_finite_compact CompleteLattice.IsSupFiniteCompact

/-- An element `k` of a complete lattice is said to be compact if any set with `sSup`
above `k` has a finite subset with `sSup` above `k`.  Such an element is also called
"finite" or "S-compact". -/
def IsCompactElement {Î± : Type*} [CompleteLattice Î±] (k : Î±) :=
  âˆ€ s : Set Î±, k â‰¤ sSup s â†’ âˆƒ t : Finset Î±, â†‘t âŠ† s âˆ§ k â‰¤ t.sup id
#align complete_lattice.is_compact_element CompleteLattice.IsCompactElement

theorem isCompactElement_iff.{u} {Î± : Type u} [CompleteLattice Î±] (k : Î±) :
    CompleteLattice.IsCompactElement k â†”
      âˆ€ (Î¹ : Type u) (s : Î¹ â†’ Î±), k â‰¤ iSup s â†’ âˆƒ t : Finset Î¹, k â‰¤ t.sup s := by
  classical
    constructor
    Â· intro H Î¹ s hs
      obtain âŸ¨t, ht, ht'âŸ© := H (Set.range s) hs
      have : âˆ€ x : t, âˆƒ i, s i = x := fun x => ht x.prop
      choose f hf using this
      refine' âŸ¨Finset.univ.image f, ht'.trans _âŸ©
      Â· rw [Finset.sup_le_iff]
        intro b hb
        rw [â† show s (f âŸ¨b, hbâŸ©) = id b from hf _]
        exact Finset.le_sup (Finset.mem_image_of_mem f <| Finset.mem_univ (Subtype.mk b hb))
    Â· intro H s hs
      obtain âŸ¨t, htâŸ© :=
        H s Subtype.val
          (by
            delta iSup
            rwa [Subtype.range_coe])
      refine' âŸ¨t.image Subtype.val, by simp, ht.trans _âŸ©
      rw [Finset.sup_le_iff]
      exact fun x hx => @Finset.le_sup _ _ _ _ _ id _ (Finset.mem_image_of_mem Subtype.val hx)
#align complete_lattice.is_compact_element_iff CompleteLattice.isCompactElement_iff

/-- An element `k` is compact if and only if any directed set with `sSup` above
`k` already got above `k` at some point in the set. -/
theorem isCompactElement_iff_le_of_directed_sSup_le (k : Î±) :
    IsCompactElement k â†”
      âˆ€ s : Set Î±, s.Nonempty â†’ DirectedOn (Â· â‰¤ Â·) s â†’ k â‰¤ sSup s â†’ âˆƒ x : Î±, x âˆˆ s âˆ§ k â‰¤ x := by
  classical
    constructor
    Â· intro hk s hne hdir hsup
      obtain âŸ¨t, htâŸ© := hk s hsup
      -- certainly every element of t is below something in s, since â†‘t âŠ† s.
      have t_below_s : âˆ€ x âˆˆ t, âˆƒ y âˆˆ s, x â‰¤ y := fun x hxt => âŸ¨x, ht.left hxt, le_rflâŸ©
      obtain âŸ¨x, âŸ¨hxs, hsupxâŸ©âŸ© := Finset.sup_le_of_le_directed s hne hdir t t_below_s
      exact âŸ¨x, âŸ¨hxs, le_trans ht.right hsupxâŸ©âŸ©
    Â· intro hk s hsup
      -- Consider the set of finite joins of elements of the (plain) set s.
      let S : Set Î± := { x | âˆƒ t : Finset Î±, â†‘t âŠ† s âˆ§ x = t.sup id }
      -- S is directed, nonempty, and still has sup above k.
      have dir_US : DirectedOn (Â· â‰¤ Â·) S := by
        rintro x âŸ¨c, hcâŸ© y âŸ¨d, hdâŸ©
        use x âŠ” y
        constructor
        Â· use c âˆª d
          constructor
          Â· simp only [hc.left, hd.left, Set.union_subset_iff, Finset.coe_union, and_self_iff]
          Â· simp only [hc.right, hd.right, Finset.sup_union]
        simp only [and_self_iff, le_sup_left, le_sup_right]
      have sup_S : sSup s â‰¤ sSup S := by
        apply sSup_le_sSup
        intro x hx
        use {x}
        simpa only [and_true_iff, id.def, Finset.coe_singleton, eq_self_iff_true,
          Finset.sup_singleton, Set.singleton_subset_iff]
      have Sne : S.Nonempty := by
        suffices : âŠ¥ âˆˆ S
        exact Set.nonempty_of_mem this
        use âˆ…
        simp only [Set.empty_subset, Finset.coe_empty, Finset.sup_empty, eq_self_iff_true,
          and_self_iff]
      -- Now apply the defn of compact and finish.
      obtain âŸ¨j, âŸ¨hjS, hjkâŸ©âŸ© := hk S Sne dir_US (le_trans hsup sup_S)
      obtain âŸ¨t, âŸ¨htS, htsupâŸ©âŸ© := hjS
      use t
      exact âŸ¨htS, by rwa [â† htsup]âŸ©
#align complete_lattice.is_compact_element_iff_le_of_directed_Sup_le CompleteLattice.isCompactElement_iff_le_of_directed_sSup_le

theorem IsCompactElement.exists_finset_of_le_iSup {k : Î±} (hk : IsCompactElement k) {Î¹ : Type*}
    (f : Î¹ â†’ Î±) (h : k â‰¤ â¨† i, f i) : âˆƒ s : Finset Î¹, k â‰¤ â¨† i âˆˆ s, f i := by
  classical
    let g : Finset Î¹ â†’ Î± := fun s => â¨† i âˆˆ s, f i
    have h1 : DirectedOn (Â· â‰¤ Â·) (Set.range g) := by
      rintro - âŸ¨s, rflâŸ© - âŸ¨t, rflâŸ©
      exact
        âŸ¨g (s âˆª t), âŸ¨s âˆª t, rflâŸ©, iSup_le_iSup_of_subset (Finset.subset_union_left s t),
          iSup_le_iSup_of_subset (Finset.subset_union_right s t)âŸ©
    have h2 : k â‰¤ sSup (Set.range g) :=
      h.trans
        (iSup_le fun i =>
          le_sSup_of_le âŸ¨{i}, rflâŸ©
            (le_iSup_of_le i (le_iSup_of_le (Finset.mem_singleton_self i) le_rfl)))
    obtain âŸ¨-, âŸ¨s, rflâŸ©, hsâŸ© :=
      (isCompactElement_iff_le_of_directed_sSup_le Î± k).mp hk (Set.range g) (Set.range_nonempty g)
        h1 h2
    exact âŸ¨s, hsâŸ©
#align complete_lattice.is_compact_element.exists_finset_of_le_supr CompleteLattice.IsCompactElement.exists_finset_of_le_iSup

/-- A compact element `k` has the property that any directed set lying strictly below `k` has
its `sSup` strictly below `k`. -/
theorem IsCompactElement.directed_sSup_lt_of_lt {Î± : Type*} [CompleteLattice Î±] {k : Î±}
    (hk : IsCompactElement k) {s : Set Î±} (hemp : s.Nonempty) (hdir : DirectedOn (Â· â‰¤ Â·) s)
    (hbelow : âˆ€ x âˆˆ s, x < k) : sSup s < k := by
  rw [isCompactElement_iff_le_of_directed_sSup_le] at hk
  -- âŠ¢ sSup s < k
  by_contra h
  -- âŠ¢ False
  have sSup' : sSup s â‰¤ k := sSup_le s k fun s hs => (hbelow s hs).le
  -- âŠ¢ False
  replace sSup : sSup s = k := eq_iff_le_not_lt.mpr âŸ¨sSup', hâŸ©
  -- âŠ¢ False
  obtain âŸ¨x, hxs, hkxâŸ© := hk s hemp hdir sSup.symm.le
  -- âŠ¢ False
  obtain hxk := hbelow x hxs
  -- âŠ¢ False
  exact hxk.ne (hxk.le.antisymm hkx)
  -- ğŸ‰ no goals
#align complete_lattice.is_compact_element.directed_Sup_lt_of_lt CompleteLattice.IsCompactElement.directed_sSup_lt_of_lt

theorem finset_sup_compact_of_compact {Î± Î² : Type*} [CompleteLattice Î±] {f : Î² â†’ Î±} (s : Finset Î²)
    (h : âˆ€ x âˆˆ s, IsCompactElement (f x)) : IsCompactElement (s.sup f) := by
  classical
    rw [isCompactElement_iff_le_of_directed_sSup_le]
    intro d hemp hdir hsup
    rw [â† Function.comp.left_id f]
    rw [â† Finset.sup_image]
    apply Finset.sup_le_of_le_directed d hemp hdir
    rintro x hx
    obtain âŸ¨p, âŸ¨hps, rflâŸ©âŸ© := Finset.mem_image.mp hx
    specialize h p hps
    rw [isCompactElement_iff_le_of_directed_sSup_le] at h
    specialize h d hemp hdir (le_trans (Finset.le_sup hps) hsup)
    simpa only [exists_prop]
#align complete_lattice.finset_sup_compact_of_compact CompleteLattice.finset_sup_compact_of_compact

theorem WellFounded.isSupFiniteCompact (h : WellFounded ((Â· > Â·) : Î± â†’ Î± â†’ Prop)) :
    IsSupFiniteCompact Î± := fun s => by
  let S := { x | âˆƒ t : Finset Î±, â†‘t âŠ† s âˆ§ t.sup id = x }
  -- âŠ¢ âˆƒ t, â†‘t âŠ† s âˆ§ sSup s = Finset.sup t id
  obtain âŸ¨m, âŸ¨t, âŸ¨htâ‚, rflâŸ©âŸ©, hmâŸ© := h.has_min S âŸ¨âŠ¥, âˆ…, by simpâŸ©
  -- âŠ¢ âˆƒ t, â†‘t âŠ† s âˆ§ sSup s = Finset.sup t id
  refine' âŸ¨t, htâ‚, (sSup_le _ _ fun y hy => _).antisymm _âŸ©
  -- âŠ¢ y â‰¤ Finset.sup t id
  Â· classical
    rw [eq_of_le_of_not_lt (Finset.sup_mono (t.subset_insert y))
        (hm _ âŸ¨insert y t, by simp [Set.insert_subset_iff, hy, htâ‚]âŸ©)]
    simp
  Â· rw [Finset.sup_id_eq_sSup]
    -- âŠ¢ sSup â†‘t â‰¤ sSup s
    exact sSup_le_sSup htâ‚
    -- ğŸ‰ no goals
#align complete_lattice.well_founded.is_Sup_finite_compact CompleteLattice.WellFounded.isSupFiniteCompact

theorem IsSupFiniteCompact.isSupClosedCompact (h : IsSupFiniteCompact Î±) :
    IsSupClosedCompact Î± := by
  intro s hne hsc; obtain âŸ¨t, htâ‚, htâ‚‚âŸ© := h s; clear h
  -- âŠ¢ sSup s âˆˆ s
                   -- âŠ¢ sSup s âˆˆ s
                                                -- âŠ¢ sSup s âˆˆ s
  cases' t.eq_empty_or_nonempty with h h
  -- âŠ¢ sSup s âˆˆ s
  Â· subst h
    -- âŠ¢ sSup s âˆˆ s
    rw [Finset.sup_empty] at htâ‚‚
    -- âŠ¢ sSup s âˆˆ s
    rw [htâ‚‚]
    -- âŠ¢ âŠ¥ âˆˆ s
    simp [eq_singleton_bot_of_sSup_eq_bot_of_nonempty htâ‚‚ hne]
    -- ğŸ‰ no goals
  Â· rw [htâ‚‚]
    -- âŠ¢ Finset.sup t id âˆˆ s
    exact t.sup_closed_of_sup_closed h htâ‚ hsc
    -- ğŸ‰ no goals
#align complete_lattice.is_Sup_finite_compact.is_sup_closed_compact CompleteLattice.IsSupFiniteCompact.isSupClosedCompact

theorem IsSupClosedCompact.wellFounded (h : IsSupClosedCompact Î±) :
    WellFounded ((Â· > Â·) : Î± â†’ Î± â†’ Prop) := by
  refine' RelEmbedding.wellFounded_iff_no_descending_seq.mpr âŸ¨fun a => _âŸ©
  -- âŠ¢ False
  suffices sSup (Set.range a) âˆˆ Set.range a by
    obtain âŸ¨n, hnâŸ© := Set.mem_range.mp this
    have h' : sSup (Set.range a) < a (n + 1) := by
      change _ > _
      simp [â† hn, a.map_rel_iff]
    apply lt_irrefl (a (n + 1))
    apply lt_of_le_of_lt _ h'
    apply le_sSup
    apply Set.mem_range_self
  apply h (Set.range a)
  -- âŠ¢ Set.Nonempty (Set.range â†‘a)
  Â· use a 37
    -- âŠ¢ â†‘a 37 âˆˆ Set.range â†‘a
    apply Set.mem_range_self
    -- ğŸ‰ no goals
  Â· rintro x âŸ¨m, hmâŸ© y âŸ¨n, hnâŸ©
    -- âŠ¢ x âŠ” y âˆˆ Set.range â†‘a
    use m âŠ” n
    -- âŠ¢ â†‘a (m âŠ” n) = x âŠ” y
    rw [â† hm, â† hn]
    -- âŠ¢ â†‘a (m âŠ” n) = â†‘a m âŠ” â†‘a n
    apply RelHomClass.map_sup a
    -- ğŸ‰ no goals
#align complete_lattice.is_sup_closed_compact.well_founded CompleteLattice.IsSupClosedCompact.wellFounded

theorem isSupFiniteCompact_iff_all_elements_compact :
    IsSupFiniteCompact Î± â†” âˆ€ k : Î±, IsCompactElement k := by
  refine' âŸ¨fun h k s hs => _, fun h s => _âŸ©
  -- âŠ¢ âˆƒ t, â†‘t âŠ† s âˆ§ k â‰¤ Finset.sup t id
  Â· obtain âŸ¨t, âŸ¨hts, htsupâŸ©âŸ© := h s
    -- âŠ¢ âˆƒ t, â†‘t âŠ† s âˆ§ k â‰¤ Finset.sup t id
    use t, hts
    -- âŠ¢ k â‰¤ Finset.sup t id
    rwa [â† htsup]
    -- ğŸ‰ no goals
  Â· obtain âŸ¨t, âŸ¨hts, htsupâŸ©âŸ© := h (sSup s) s (by rfl)
    -- âŠ¢ âˆƒ t, â†‘t âŠ† s âˆ§ sSup s = Finset.sup t id
    have : sSup s = t.sup id := by
      suffices t.sup id â‰¤ sSup s by apply le_antisymm <;> assumption
      simp only [id.def, Finset.sup_le_iff]
      intro x hx
      exact le_sSup _ _ (hts hx)
    exact âŸ¨t, hts, thisâŸ©
    -- ğŸ‰ no goals
#align complete_lattice.is_Sup_finite_compact_iff_all_elements_compact CompleteLattice.isSupFiniteCompact_iff_all_elements_compact

open List in
theorem wellFounded_characterisations : List.TFAE
    [WellFounded (( Â· > Â· ) : Î± â†’ Î± â†’ Prop),
      IsSupFiniteCompact Î±, IsSupClosedCompact Î±, âˆ€ k : Î±, IsCompactElement k] := by
  tfae_have 1 â†’ 2
  -- âŠ¢ (WellFounded fun x x_1 => x > x_1) â†’ IsSupFiniteCompact Î±
  Â· exact WellFounded.isSupFiniteCompact Î±
    -- ğŸ‰ no goals
  tfae_have 2 â†’ 3
  -- âŠ¢ IsSupFiniteCompact Î± â†’ IsSupClosedCompact Î±
  Â· exact IsSupFiniteCompact.isSupClosedCompact Î±
    -- ğŸ‰ no goals
  tfae_have 3 â†’ 1
  -- âŠ¢ IsSupClosedCompact Î± â†’ WellFounded fun x x_1 => x > x_1
  Â· exact IsSupClosedCompact.wellFounded Î±
    -- ğŸ‰ no goals
  tfae_have 2 â†” 4
  -- âŠ¢ IsSupFiniteCompact Î± â†” âˆ€ (k : Î±), IsCompactElement k
  Â· exact isSupFiniteCompact_iff_all_elements_compact Î±
    -- ğŸ‰ no goals
  tfae_finish
  -- ğŸ‰ no goals
#align complete_lattice.well_founded_characterisations CompleteLattice.wellFounded_characterisations

theorem wellFounded_iff_isSupFiniteCompact :
    WellFounded ((Â· > Â·) : Î± â†’ Î± â†’ Prop) â†” IsSupFiniteCompact Î± :=
  (wellFounded_characterisations Î±).out 0 1
#align complete_lattice.well_founded_iff_is_Sup_finite_compact CompleteLattice.wellFounded_iff_isSupFiniteCompact

theorem isSupFiniteCompact_iff_isSupClosedCompact : IsSupFiniteCompact Î± â†” IsSupClosedCompact Î± :=
  (wellFounded_characterisations Î±).out 1 2
#align complete_lattice.is_Sup_finite_compact_iff_is_sup_closed_compact CompleteLattice.isSupFiniteCompact_iff_isSupClosedCompact

theorem isSupClosedCompact_iff_wellFounded :
    IsSupClosedCompact Î± â†” WellFounded ((Â· > Â·) : Î± â†’ Î± â†’ Prop) :=
  (wellFounded_characterisations Î±).out 2 0
#align complete_lattice.is_sup_closed_compact_iff_well_founded CompleteLattice.isSupClosedCompact_iff_wellFounded

alias âŸ¨_, IsSupFiniteCompact.wellFoundedâŸ© := wellFounded_iff_isSupFiniteCompact
#align complete_lattice.is_Sup_finite_compact.well_founded CompleteLattice.IsSupFiniteCompact.wellFounded

alias âŸ¨_, IsSupClosedCompact.isSupFiniteCompactâŸ© := isSupFiniteCompact_iff_isSupClosedCompact
#align complete_lattice.is_sup_closed_compact.is_Sup_finite_compact CompleteLattice.IsSupClosedCompact.isSupFiniteCompact

alias âŸ¨_, _root_.WellFounded.isSupClosedCompactâŸ© := isSupClosedCompact_iff_wellFounded
#align well_founded.is_sup_closed_compact WellFounded.isSupClosedCompact

variable {Î±}

theorem WellFounded.finite_of_setIndependent (h : WellFounded ((Â· > Â·) : Î± â†’ Î± â†’ Prop)) {s : Set Î±}
    (hs : SetIndependent s) : s.Finite := by
  classical
    refine' Set.not_infinite.mp fun contra => _
    obtain âŸ¨t, htâ‚, htâ‚‚âŸ© := WellFounded.isSupFiniteCompact Î± h s
    replace contra : âˆƒ x : Î±, x âˆˆ s âˆ§ x â‰  âŠ¥ âˆ§ x âˆ‰ t
    Â· have : (s \ (insert âŠ¥ t : Finset Î±)).Infinite := contra.diff (Finset.finite_toSet _)
      obtain âŸ¨x, hxâ‚, hxâ‚‚âŸ© := this.nonempty
      exact âŸ¨x, hxâ‚, by simpa [not_or] using hxâ‚‚âŸ©
    obtain âŸ¨x, hxâ‚€, hxâ‚, hxâ‚‚âŸ© := contra
    replace hs : x âŠ“ sSup s = âŠ¥
    Â· have := hs.mono (by simp [htâ‚, hxâ‚€, -Set.union_singleton] : â†‘t âˆª {x} â‰¤ s) (by simp : x âˆˆ _)
      simpa [Disjoint, hxâ‚‚, â† t.sup_id_eq_sSup, â† htâ‚‚] using this.eq_bot
    apply hxâ‚
    rw [â† hs, eq_comm, inf_eq_left]
    exact le_sSup _ _ hxâ‚€
#align complete_lattice.well_founded.finite_of_set_independent CompleteLattice.WellFounded.finite_of_setIndependent

theorem WellFounded.finite_of_independent (hwf : WellFounded ((Â· > Â·) : Î± â†’ Î± â†’ Prop)) {Î¹ : Type*}
    {t : Î¹ â†’ Î±} (ht : Independent t) (h_ne_bot : âˆ€ i, t i â‰  âŠ¥) : Finite Î¹ :=
  haveI := (WellFounded.finite_of_setIndependent hwf ht.setIndependent_range).to_subtype
  Finite.of_injective_finite_range (ht.injective h_ne_bot)
#align complete_lattice.well_founded.finite_of_independent CompleteLattice.WellFounded.finite_of_independent

end CompleteLattice

/-- A complete lattice is said to be compactly generated if any
element is the `sSup` of compact elements. -/
class IsCompactlyGenerated (Î± : Type*) [CompleteLattice Î±] : Prop where
  /-- In a compactly generated complete lattice,
    every element is the `sSup` of some set of compact elements. -/
  exists_sSup_eq : âˆ€ x : Î±, âˆƒ s : Set Î±, (âˆ€ x âˆˆ s, CompleteLattice.IsCompactElement x) âˆ§ sSup s = x
#align is_compactly_generated IsCompactlyGenerated

section

variable [CompleteLattice Î±] [IsCompactlyGenerated Î±] {a b : Î±} {s : Set Î±}

@[simp]
theorem sSup_compact_le_eq (b) :
    sSup { c : Î± | CompleteLattice.IsCompactElement c âˆ§ c â‰¤ b } = b := by
  rcases IsCompactlyGenerated.exists_sSup_eq b with âŸ¨s, hs, rflâŸ©
  -- âŠ¢ sSup {c | CompleteLattice.IsCompactElement c âˆ§ c â‰¤ sSup s} = sSup s
  exact le_antisymm (sSup_le fun c hc => hc.2) (sSup_le_sSup fun c cs => âŸ¨hs c cs, le_sSup csâŸ©)
  -- ğŸ‰ no goals
#align Sup_compact_le_eq sSup_compact_le_eq

@[simp]
theorem sSup_compact_eq_top : sSup { a : Î± | CompleteLattice.IsCompactElement a } = âŠ¤ := by
  refine' Eq.trans (congr rfl (Set.ext fun x => _)) (sSup_compact_le_eq âŠ¤)
  -- âŠ¢ x âˆˆ {a | CompleteLattice.IsCompactElement a} â†” x âˆˆ {c | CompleteLattice.IsCo â€¦
  exact (and_iff_left le_top).symm
  -- ğŸ‰ no goals
#align Sup_compact_eq_top sSup_compact_eq_top

theorem le_iff_compact_le_imp {a b : Î±} :
    a â‰¤ b â†” âˆ€ c : Î±, CompleteLattice.IsCompactElement c â†’ c â‰¤ a â†’ c â‰¤ b :=
  âŸ¨fun ab c _ ca => le_trans ca ab, fun h => by
    rw [â† sSup_compact_le_eq a, â† sSup_compact_le_eq b]
    -- âŠ¢ sSup {c | CompleteLattice.IsCompactElement c âˆ§ c â‰¤ a} â‰¤ sSup {c | CompleteLa â€¦
    exact sSup_le_sSup fun c hc => âŸ¨hc.1, h c hc.1 hc.2âŸ©âŸ©
    -- ğŸ‰ no goals
#align le_iff_compact_le_imp le_iff_compact_le_imp

/-- This property is sometimes referred to as `Î±` being upper continuous. -/
theorem DirectedOn.inf_sSup_eq (h : DirectedOn (Â· â‰¤ Â·) s) : a âŠ“ sSup s = â¨† b âˆˆ s, a âŠ“ b :=
  le_antisymm
    (by
      rw [le_iff_compact_le_imp]
      -- âŠ¢ âˆ€ (c : Î±), CompleteLattice.IsCompactElement c â†’ c â‰¤ a âŠ“ sSup s â†’ c â‰¤ â¨† (b :  â€¦
      by_cases hs : s.Nonempty
      -- âŠ¢ âˆ€ (c : Î±), CompleteLattice.IsCompactElement c â†’ c â‰¤ a âŠ“ sSup s â†’ c â‰¤ â¨† (b :  â€¦
      Â· intro c hc hcinf
        -- âŠ¢ c â‰¤ â¨† (b : Î±) (_ : b âˆˆ s), a âŠ“ b
        rw [le_inf_iff] at hcinf
        -- âŠ¢ c â‰¤ â¨† (b : Î±) (_ : b âˆˆ s), a âŠ“ b
        rw [CompleteLattice.isCompactElement_iff_le_of_directed_sSup_le] at hc
        -- âŠ¢ c â‰¤ â¨† (b : Î±) (_ : b âˆˆ s), a âŠ“ b
        rcases hc s hs h hcinf.2 with âŸ¨d, ds, cdâŸ©
        -- âŠ¢ c â‰¤ â¨† (b : Î±) (_ : b âˆˆ s), a âŠ“ b
        refine' (le_inf hcinf.1 cd).trans (le_trans _ (le_iSupâ‚‚ d ds))
        -- âŠ¢ a âŠ“ d â‰¤ a âŠ“ d
        rfl
        -- ğŸ‰ no goals
      Â· rw [Set.not_nonempty_iff_eq_empty] at hs
        -- âŠ¢ âˆ€ (c : Î±), CompleteLattice.IsCompactElement c â†’ c â‰¤ a âŠ“ sSup s â†’ c â‰¤ â¨† (b :  â€¦
        simp [hs])
        -- ğŸ‰ no goals
    iSup_inf_le_inf_sSup
#align directed_on.inf_Sup_eq DirectedOn.inf_sSup_eq

/-- This property is sometimes referred to as `Î±` being upper continuous. -/
protected theorem DirectedOn.sSup_inf_eq (h : DirectedOn (Â· â‰¤ Â·) s) : sSup s âŠ“ a = â¨† b âˆˆ s, b âŠ“ a :=
  by simp_rw [@inf_comm _ _ _ a, h.inf_sSup_eq]
     -- ğŸ‰ no goals
#align directed_on.Sup_inf_eq DirectedOn.sSup_inf_eq

protected theorem Directed.inf_iSup_eq (h : Directed (Â· â‰¤ Â·) f) : (a âŠ“ â¨† i, f i) = â¨† i, a âŠ“ f i :=
  by rw [iSup, h.directedOn_range.inf_sSup_eq, iSup_range]
     -- ğŸ‰ no goals
#align directed.inf_supr_eq Directed.inf_iSup_eq

protected theorem Directed.iSup_inf_eq (h : Directed (Â· â‰¤ Â·) f) : (â¨† i, f i) âŠ“ a = â¨† i, f i âŠ“ a :=
  by rw [iSup, h.directedOn_range.sSup_inf_eq, iSup_range]
     -- ğŸ‰ no goals
#align directed.supr_inf_eq Directed.iSup_inf_eq

protected theorem DirectedOn.disjoint_sSup_right (h : DirectedOn (Â· â‰¤ Â·) s) :
    Disjoint a (sSup s) â†” âˆ€ â¦ƒbâ¦„, b âˆˆ s â†’ Disjoint a b := by
  simp_rw [disjoint_iff, h.inf_sSup_eq, iSup_eq_bot]
  -- ğŸ‰ no goals
#align directed_on.disjoint_Sup_right DirectedOn.disjoint_sSup_right

protected theorem DirectedOn.disjoint_sSup_left (h : DirectedOn (Â· â‰¤ Â·) s) :
    Disjoint (sSup s) a â†” âˆ€ â¦ƒbâ¦„, b âˆˆ s â†’ Disjoint b a := by
  simp_rw [disjoint_iff, h.sSup_inf_eq, iSup_eq_bot]
  -- ğŸ‰ no goals
#align directed_on.disjoint_Sup_left DirectedOn.disjoint_sSup_left

protected theorem Directed.disjoint_iSup_right (h : Directed (Â· â‰¤ Â·) f) :
    Disjoint a (â¨† i, f i) â†” âˆ€ i, Disjoint a (f i) := by
  simp_rw [disjoint_iff, h.inf_iSup_eq, iSup_eq_bot]
  -- ğŸ‰ no goals
#align directed.disjoint_supr_right Directed.disjoint_iSup_right

protected theorem Directed.disjoint_iSup_left (h : Directed (Â· â‰¤ Â·) f) :
    Disjoint (â¨† i, f i) a â†” âˆ€ i, Disjoint (f i) a := by
  simp_rw [disjoint_iff, h.iSup_inf_eq, iSup_eq_bot]
  -- ğŸ‰ no goals
#align directed.disjoint_supr_left Directed.disjoint_iSup_left

/-- This property is equivalent to `Î±` being upper continuous. -/
theorem inf_sSup_eq_iSup_inf_sup_finset :
    a âŠ“ sSup s = â¨† (t : Finset Î±) (_ : â†‘t âŠ† s), a âŠ“ t.sup id :=
  le_antisymm
    (by
      rw [le_iff_compact_le_imp]
      -- âŠ¢ âˆ€ (c : Î±), CompleteLattice.IsCompactElement c â†’ c â‰¤ a âŠ“ sSup s â†’ c â‰¤ â¨† (t :  â€¦
      intro c hc hcinf
      -- âŠ¢ c â‰¤ â¨† (t : Finset Î±) (_ : â†‘t âŠ† s), a âŠ“ Finset.sup t id
      rw [le_inf_iff] at hcinf
      -- âŠ¢ c â‰¤ â¨† (t : Finset Î±) (_ : â†‘t âŠ† s), a âŠ“ Finset.sup t id
      rcases hc s hcinf.2 with âŸ¨t, ht1, ht2âŸ©
      -- âŠ¢ c â‰¤ â¨† (t : Finset Î±) (_ : â†‘t âŠ† s), a âŠ“ Finset.sup t id
      refine' (le_inf hcinf.1 ht2).trans (le_trans _ (le_iSupâ‚‚ t ht1))
      -- âŠ¢ a âŠ“ Finset.sup t id â‰¤ a âŠ“ Finset.sup t id
      rfl)
      -- ğŸ‰ no goals
    (iSup_le fun t =>
      iSup_le fun h => inf_le_inf_left _ ((Finset.sup_id_eq_sSup t).symm â–¸ sSup_le_sSup h))
#align inf_Sup_eq_supr_inf_sup_finset inf_sSup_eq_iSup_inf_sup_finset

theorem CompleteLattice.setIndependent_iff_finite {s : Set Î±} :
    CompleteLattice.SetIndependent s â†”
      âˆ€ t : Finset Î±, â†‘t âŠ† s â†’ CompleteLattice.SetIndependent (â†‘t : Set Î±) :=
  âŸ¨fun hs t ht => hs.mono ht, fun h a ha => by
    rw [disjoint_iff, inf_sSup_eq_iSup_inf_sup_finset, iSup_eq_bot]
    -- âŠ¢ âˆ€ (i : Finset Î±), â¨† (_ : â†‘i âŠ† s \ {a}), a âŠ“ Finset.sup i id = âŠ¥
    intro t
    -- âŠ¢ â¨† (_ : â†‘t âŠ† s \ {a}), a âŠ“ Finset.sup t id = âŠ¥
    rw [iSup_eq_bot, Finset.sup_id_eq_sSup]
    -- âŠ¢ â†‘t âŠ† s \ {a} â†’ a âŠ“ sSup â†‘t = âŠ¥
    intro ht
    -- âŠ¢ a âŠ“ sSup â†‘t = âŠ¥
    classical
      have h' := (h (insert a t) ?_ (t.mem_insert_self a)).eq_bot
      Â· rwa [Finset.coe_insert, Set.insert_diff_self_of_not_mem] at h'
        exact fun con => ((Set.mem_diff a).1 (ht con)).2 (Set.mem_singleton a)
      Â· rw [Finset.coe_insert, Set.insert_subset_iff]
        exact âŸ¨ha, Set.Subset.trans ht (Set.diff_subset _ _)âŸ©âŸ©
#align complete_lattice.set_independent_iff_finite CompleteLattice.setIndependent_iff_finite

theorem CompleteLattice.setIndependent_iUnion_of_directed {Î· : Type*} {s : Î· â†’ Set Î±}
    (hs : Directed (Â· âŠ† Â·) s) (h : âˆ€ i, CompleteLattice.SetIndependent (s i)) :
    CompleteLattice.SetIndependent (â‹ƒ i, s i) := by
  by_cases hÎ· : Nonempty Î·
  -- âŠ¢ SetIndependent (â‹ƒ (i : Î·), s i)
  Â· rw [CompleteLattice.setIndependent_iff_finite]
    -- âŠ¢ âˆ€ (t : Finset Î±), â†‘t âŠ† â‹ƒ (i : Î·), s i â†’ SetIndependent â†‘t
    intro t ht
    -- âŠ¢ SetIndependent â†‘t
    obtain âŸ¨I, fi, hIâŸ© := Set.finite_subset_iUnion t.finite_toSet ht
    -- âŠ¢ SetIndependent â†‘t
    obtain âŸ¨i, hiâŸ© := hs.finset_le fi.toFinset
    -- âŠ¢ SetIndependent â†‘t
    exact (h i).mono
        (Set.Subset.trans hI <| Set.iUnionâ‚‚_subset fun j hj => hi j (fi.mem_toFinset.2 hj))
  Â· rintro a âŸ¨_, âŸ¨i, _âŸ©, _âŸ©
    -- âŠ¢ Disjoint a (sSup ((â‹ƒ (i : Î·), s i) \ {a}))
    exfalso
    -- âŠ¢ False
    exact hÎ· âŸ¨iâŸ©
    -- ğŸ‰ no goals
#align complete_lattice.set_independent_Union_of_directed CompleteLattice.setIndependent_iUnion_of_directed

theorem CompleteLattice.independent_sUnion_of_directed {s : Set (Set Î±)} (hs : DirectedOn (Â· âŠ† Â·) s)
    (h : âˆ€ a âˆˆ s, CompleteLattice.SetIndependent a) : CompleteLattice.SetIndependent (â‹ƒâ‚€ s) := by
  rw [Set.sUnion_eq_iUnion]
  -- âŠ¢ SetIndependent (â‹ƒ (i : â†‘s), â†‘i)
  exact CompleteLattice.setIndependent_iUnion_of_directed hs.directed_val (by simpa using h)
  -- ğŸ‰ no goals
#align complete_lattice.independent_sUnion_of_directed CompleteLattice.independent_sUnion_of_directed

end

namespace CompleteLattice

theorem isCompactlyGenerated_of_wellFounded (h : WellFounded ((Â· > Â·) : Î± â†’ Î± â†’ Prop)) :
    IsCompactlyGenerated Î± := by
  rw [wellFounded_iff_isSupFiniteCompact, isSupFiniteCompact_iff_all_elements_compact] at h
  -- âŠ¢ IsCompactlyGenerated Î±
  -- x is the join of the set of compact elements {x}
  exact âŸ¨fun x => âŸ¨{x}, âŸ¨fun x _ => h x, sSup_singletonâŸ©âŸ©âŸ©
  -- ğŸ‰ no goals
#align complete_lattice.compactly_generated_of_well_founded CompleteLattice.isCompactlyGenerated_of_wellFounded

/-- A compact element `k` has the property that any `b < k` lies below a "maximal element below
`k`", which is to say `[âŠ¥, k]` is coatomic. -/
theorem Iic_coatomic_of_compact_element {k : Î±} (h : IsCompactElement k) :
    IsCoatomic (Set.Iic k) := by
  constructor
  -- âŠ¢ âˆ€ (b : â†‘(Set.Iic k)), b = âŠ¤ âˆ¨ âˆƒ a, IsCoatom a âˆ§ b â‰¤ a
  rintro âŸ¨b, hbkâŸ©
  -- âŠ¢ { val := b, property := hbk } = âŠ¤ âˆ¨ âˆƒ a, IsCoatom a âˆ§ { val := b, property : â€¦
  obtain rfl | H := eq_or_ne b k
  -- âŠ¢ { val := b, property := hbk } = âŠ¤ âˆ¨ âˆƒ a, IsCoatom a âˆ§ { val := b, property : â€¦
  Â· left; ext; simp only [Set.Iic.coe_top, Subtype.coe_mk]
    -- âŠ¢ { val := b, property := hbk } = âŠ¤
          -- âŠ¢ â†‘{ val := b, property := hbk } = â†‘âŠ¤
               -- ğŸ‰ no goals
  right
  -- âŠ¢ âˆƒ a, IsCoatom a âˆ§ { val := b, property := hbk } â‰¤ a
  have âŸ¨a, aâ‚€, ba, hâŸ© := zorn_nonempty_partialOrderâ‚€ (Set.Iio k) ?_ b (lt_of_le_of_ne hbk H)
  -- âŠ¢ âˆƒ a, IsCoatom a âˆ§ { val := b, property := hbk } â‰¤ a
  Â· refine' âŸ¨âŸ¨a, le_of_lt aâ‚€âŸ©, âŸ¨ne_of_lt aâ‚€, fun c hck => by_contradiction fun câ‚€ => _âŸ©, baâŸ©
    -- âŠ¢ False
    cases h c.1 (lt_of_le_of_ne c.2 fun con => câ‚€ (Subtype.ext con)) hck.le
    -- âŠ¢ False
    exact lt_irrefl _ hck
    -- ğŸ‰ no goals
  Â· intro S SC cC I _
    -- âŠ¢ âˆƒ ub, ub âˆˆ Set.Iio k âˆ§ âˆ€ (z : Î±), z âˆˆ S â†’ z â‰¤ ub
    by_cases hS : S.Nonempty
    -- âŠ¢ âˆƒ ub, ub âˆˆ Set.Iio k âˆ§ âˆ€ (z : Î±), z âˆˆ S â†’ z â‰¤ ub
    Â· refine' âŸ¨sSup S, h.directed_sSup_lt_of_lt hS cC.directedOn SC, _âŸ©
      -- âŠ¢ âˆ€ (z : Î±), z âˆˆ S â†’ z â‰¤ sSup S
      intro; apply le_sSup
      -- âŠ¢ zâœ âˆˆ S â†’ zâœ â‰¤ sSup S
             -- ğŸ‰ no goals
    exact
      âŸ¨b, lt_of_le_of_ne hbk H, by
        simp only [Set.not_nonempty_iff_eq_empty.mp hS, Set.mem_empty_iff_false, forall_const,
          forall_prop_of_false, not_false_iff]âŸ©
#align complete_lattice.Iic_coatomic_of_compact_element CompleteLattice.Iic_coatomic_of_compact_element

theorem coatomic_of_top_compact (h : IsCompactElement (âŠ¤ : Î±)) : IsCoatomic Î± :=
  (@OrderIso.IicTop Î± _ _).isCoatomic_iff.mp (Iic_coatomic_of_compact_element h)
#align complete_lattice.coatomic_of_top_compact CompleteLattice.coatomic_of_top_compact

end CompleteLattice

section

variable [IsModularLattice Î±] [IsCompactlyGenerated Î±]

instance (priority := 100) isAtomic_of_complementedLattice [ComplementedLattice Î±] : IsAtomic Î± :=
  âŸ¨fun b => by
    by_cases h : { c : Î± | CompleteLattice.IsCompactElement c âˆ§ c â‰¤ b } âŠ† {âŠ¥}
    -- âŠ¢ b = âŠ¥ âˆ¨ âˆƒ a, IsAtom a âˆ§ a â‰¤ b
    Â· left
      -- âŠ¢ b = âŠ¥
      rw [â† sSup_compact_le_eq b, sSup_eq_bot]
      -- âŠ¢ âˆ€ (a : Î±), a âˆˆ {c | CompleteLattice.IsCompactElement c âˆ§ c â‰¤ b} â†’ a = âŠ¥
      exact h
      -- ğŸ‰ no goals
    Â· rcases Set.not_subset.1 h with âŸ¨c, âŸ¨hc, hcbâŸ©, hcbotâŸ©
      -- âŠ¢ b = âŠ¥ âˆ¨ âˆƒ a, IsAtom a âˆ§ a â‰¤ b
      right
      -- âŠ¢ âˆƒ a, IsAtom a âˆ§ a â‰¤ b
      have hc' := CompleteLattice.Iic_coatomic_of_compact_element hc
      -- âŠ¢ âˆƒ a, IsAtom a âˆ§ a â‰¤ b
      rw [â† isAtomic_iff_isCoatomic] at hc'
      -- âŠ¢ âˆƒ a, IsAtom a âˆ§ a â‰¤ b
      haveI := hc'
      -- âŠ¢ âˆƒ a, IsAtom a âˆ§ a â‰¤ b
      obtain con | âŸ¨a, ha, hacâŸ© := eq_bot_or_exists_atom_le (âŸ¨c, le_refl câŸ© : Set.Iic c)
      -- âŠ¢ âˆƒ a, IsAtom a âˆ§ a â‰¤ b
      Â· exfalso
        -- âŠ¢ False
        apply hcbot
        -- âŠ¢ c âˆˆ {âŠ¥}
        simp only [Subtype.ext_iff, Set.Iic.coe_bot, Subtype.coe_mk] at con
        -- âŠ¢ c âˆˆ {âŠ¥}
        exact con
        -- ğŸ‰ no goals
      rw [â† Subtype.coe_le_coe, Subtype.coe_mk] at hac
      -- âŠ¢ âˆƒ a, IsAtom a âˆ§ a â‰¤ b
      exact âŸ¨a, ha.of_isAtom_coe_Iic, hac.trans hcbâŸ©âŸ©
      -- ğŸ‰ no goals
#align is_atomic_of_complemented_lattice isAtomic_of_complementedLattice

/-- See [Lemma 5.1][calugareanu]. -/
instance (priority := 100) isAtomistic_of_complementedLattice [ComplementedLattice Î±] :
    IsAtomistic Î± :=
  âŸ¨fun b =>
    âŸ¨{ a | IsAtom a âˆ§ a â‰¤ b }, by
      symm
      -- âŠ¢ sSup {a | IsAtom a âˆ§ a â‰¤ b} = b
      have hle : sSup { a : Î± | IsAtom a âˆ§ a â‰¤ b } â‰¤ b := sSup_le fun _ => And.right
      -- âŠ¢ sSup {a | IsAtom a âˆ§ a â‰¤ b} = b
      apply (lt_or_eq_of_le hle).resolve_left _
      -- âŠ¢ Â¬sSup {a | IsAtom a âˆ§ a â‰¤ b} < b
      intro con
      -- âŠ¢ False
      obtain âŸ¨c, hcâŸ© := exists_isCompl (âŸ¨sSup { a : Î± | IsAtom a âˆ§ a â‰¤ b }, hleâŸ© : Set.Iic b)
      -- âŠ¢ False
      obtain rfl | âŸ¨a, ha, hacâŸ© := eq_bot_or_exists_atom_le c
      -- âŠ¢ False
      Â· exact ne_of_lt con (Subtype.ext_iff.1 (eq_top_of_isCompl_bot hc))
        -- ğŸ‰ no goals
      Â· apply ha.1
        -- âŠ¢ a = âŠ¥
        rw [eq_bot_iff]
        -- âŠ¢ a â‰¤ âŠ¥
        apply le_trans (le_inf _ hac) hc.disjoint.le_bot
        -- âŠ¢ a â‰¤ { val := sSup {a | IsAtom a âˆ§ a â‰¤ b}, property := hle }
        rw [â† Subtype.coe_le_coe, Subtype.coe_mk]
        -- âŠ¢ â†‘a â‰¤ â†‘{ val := sSup {a | IsAtom a âˆ§ a â‰¤ b}, property := hle }
        exact le_sSup âŸ¨ha.of_isAtom_coe_Iic, a.2âŸ©, fun _ => And.leftâŸ©âŸ©
        -- ğŸ‰ no goals
#align is_atomistic_of_complemented_lattice isAtomistic_of_complementedLattice

/-!
Now we will prove that a compactly generated modular atomistic lattice is a complemented lattice.
Most explicitly, every element is the complement of a supremum of indepedendent atoms.
-/

/-- In an atomic lattice, every element `b` has a complement of the form `sSup s`, where each
element of `s` is an atom. See also `complementedLattice_of_sSup_atoms_eq_top`. -/
theorem exists_setIndependent_isCompl_sSup_atoms (h : sSup { a : Î± | IsAtom a } = âŠ¤) (b : Î±) :
    âˆƒ s : Set Î±, CompleteLattice.SetIndependent s âˆ§
    IsCompl b (sSup s) âˆ§ âˆ€ â¦ƒaâ¦„, a âˆˆ s â†’ IsAtom a := by
  -- porting note(https://github.com/leanprover-community/mathlib4/issues/5732):
  -- `obtain` chokes on the placeholder.
  have := zorn_subset
    {s : Set Î± | CompleteLattice.SetIndependent s âˆ§ Disjoint b (sSup s) âˆ§ âˆ€ a âˆˆ s, IsAtom a}
    fun c hc1 hc2 =>
      âŸ¨â‹ƒâ‚€ c,
        âŸ¨CompleteLattice.independent_sUnion_of_directed hc2.directedOn fun s hs => (hc1 hs).1, ?_,
          fun a âŸ¨s, sc, asâŸ© => (hc1 sc).2.2 a asâŸ©,
        fun _ => Set.subset_sUnion_of_memâŸ©
  obtain âŸ¨s, âŸ¨s_ind, b_inf_Sup_s, s_atomsâŸ©, s_maxâŸ© := this
  -- âŠ¢ âˆƒ s, CompleteLattice.SetIndependent s âˆ§ IsCompl b (sSup s) âˆ§ âˆ€ â¦ƒa : Î±â¦„, a âˆˆ  â€¦
  swap
  -- âŠ¢ Disjoint b (sSup (â‹ƒâ‚€ c))
  Â· rw [sSup_sUnion, â† sSup_image, DirectedOn.disjoint_sSup_right]
    -- âŠ¢ âˆ€ â¦ƒb_1 : Î±â¦„, b_1 âˆˆ (fun t => sSup t) '' c â†’ Disjoint b b_1
    Â· rintro _ âŸ¨s, hs, rflâŸ©
      -- âŠ¢ Disjoint b ((fun t => sSup t) s)
      exact (hc1 hs).2.1
      -- ğŸ‰ no goals
    Â· rw [directedOn_image]
      -- âŠ¢ DirectedOn ((fun t => sSup t) â»Â¹'o fun x x_1 => x â‰¤ x_1) c
      exact hc2.directedOn.mono @fun s t => sSup_le_sSup
      -- ğŸ‰ no goals
  refine' âŸ¨s, s_ind, âŸ¨b_inf_Sup_s, _âŸ©, s_atomsâŸ©
  -- âŠ¢ Codisjoint b (sSup s)
  rw [codisjoint_iff_le_sup, â† h, sSup_le_iff]
  -- âŠ¢ âˆ€ (b_1 : Î±), b_1 âˆˆ {a | IsAtom a} â†’ b_1 â‰¤ b âŠ” sSup s
  intro a ha
  -- âŠ¢ a â‰¤ b âŠ” sSup s
  rw [â† inf_eq_left]
  -- âŠ¢ a âŠ“ (b âŠ” sSup s) = a
  refine' (ha.le_iff.mp inf_le_left).resolve_left fun con => ha.1 _
  -- âŠ¢ a = âŠ¥
  rw [â† con, eq_comm, inf_eq_left]
  -- âŠ¢ a â‰¤ b âŠ” sSup s
  refine' (le_sSup _).trans le_sup_right
  -- âŠ¢ a âˆˆ s
  rw [â† disjoint_iff] at con
  -- âŠ¢ a âˆˆ s
  have a_dis_Sup_s : Disjoint a (sSup s) := con.mono_right le_sup_right
  -- âŠ¢ a âˆˆ s
  -- porting note: The two following `fun x hx => _` are no-op
  rw [â† s_max (s âˆª {a}) âŸ¨fun x hx => _, _, fun x hx => _âŸ© (Set.subset_union_left _ _)]
  Â· exact Set.mem_union_right _ (Set.mem_singleton _)
    -- ğŸ‰ no goals
  Â· intro x hx
    -- âŠ¢ Disjoint x (sSup ((s âˆª {a}) \ {x}))
    rw [Set.mem_union, Set.mem_singleton_iff] at hx
    -- âŠ¢ Disjoint x (sSup ((s âˆª {a}) \ {x}))
    obtain rfl | xa := eq_or_ne x a
    -- âŠ¢ Disjoint x (sSup ((s âˆª {x}) \ {x}))
    Â· simp only [Set.mem_singleton, Set.insert_diff_of_mem, Set.union_singleton]
      -- âŠ¢ Disjoint x (sSup (s \ {x}))
      exact con.mono_right ((sSup_le_sSup <| Set.diff_subset _ _).trans le_sup_right)
      -- ğŸ‰ no goals
    Â· have h : (s âˆª {a}) \ {x} = s \ {x} âˆª {a} := by
        simp only [Set.union_singleton]
        rw [Set.insert_diff_of_not_mem]
        rw [Set.mem_singleton_iff]
        exact Ne.symm xa
      rw [h, sSup_union, sSup_singleton]
      -- âŠ¢ Disjoint x (sSup (s \ {x}) âŠ” a)
      apply
        (s_ind (hx.resolve_right xa)).disjoint_sup_right_of_disjoint_sup_left
          (a_dis_Sup_s.mono_right _).symm
      rw [â† sSup_insert, Set.insert_diff_singleton, Set.insert_eq_of_mem (hx.resolve_right xa)]
      -- ğŸ‰ no goals
  Â· rw [sSup_union, sSup_singleton]
    -- âŠ¢ Disjoint b (sSup s âŠ” a)
    exact b_inf_Sup_s.disjoint_sup_right_of_disjoint_sup_left con.symm
    -- ğŸ‰ no goals
  Â· intro x hx
    -- âŠ¢ IsAtom x
    rw [Set.mem_union, Set.mem_singleton_iff] at hx
    -- âŠ¢ IsAtom x
    obtain hx | rfl := hx
    -- âŠ¢ IsAtom x
    Â· exact s_atoms x hx
      -- ğŸ‰ no goals
    Â· exact ha
      -- ğŸ‰ no goals
#align exists_set_independent_is_compl_Sup_atoms exists_setIndependent_isCompl_sSup_atoms

theorem exists_setIndependent_of_sSup_atoms_eq_top (h : sSup { a : Î± | IsAtom a } = âŠ¤) :
    âˆƒ s : Set Î±, CompleteLattice.SetIndependent s âˆ§ sSup s = âŠ¤ âˆ§ âˆ€ â¦ƒaâ¦„, a âˆˆ s â†’ IsAtom a :=
  let âŸ¨s, s_ind, s_top, s_atomsâŸ© := exists_setIndependent_isCompl_sSup_atoms h âŠ¥
  âŸ¨s, s_ind, eq_top_of_isCompl_bot s_top.symm, s_atomsâŸ©
#align exists_set_independent_of_Sup_atoms_eq_top exists_setIndependent_of_sSup_atoms_eq_top

/-- See [Theorem 6.6][calugareanu]. -/
theorem complementedLattice_of_sSup_atoms_eq_top (h : sSup { a : Î± | IsAtom a } = âŠ¤) :
    ComplementedLattice Î± :=
  âŸ¨fun b =>
    let âŸ¨s, _, s_top, _âŸ© := exists_setIndependent_isCompl_sSup_atoms h b
    âŸ¨sSup s, s_topâŸ©âŸ©
#align complemented_lattice_of_Sup_atoms_eq_top complementedLattice_of_sSup_atoms_eq_top

/-- See [Theorem 6.6][calugareanu]. -/
theorem complementedLattice_of_isAtomistic [IsAtomistic Î±] : ComplementedLattice Î± :=
  complementedLattice_of_sSup_atoms_eq_top sSup_atoms_eq_top
#align complemented_lattice_of_is_atomistic complementedLattice_of_isAtomistic

theorem complementedLattice_iff_isAtomistic : ComplementedLattice Î± â†” IsAtomistic Î± := by
  constructor <;> intros
  -- âŠ¢ ComplementedLattice Î± â†’ IsAtomistic Î±
                  -- âŠ¢ IsAtomistic Î±
                  -- âŠ¢ ComplementedLattice Î±
  Â· exact isAtomistic_of_complementedLattice
    -- ğŸ‰ no goals
  Â· exact complementedLattice_of_isAtomistic
    -- ğŸ‰ no goals
#align complemented_lattice_iff_is_atomistic complementedLattice_iff_isAtomistic

end
