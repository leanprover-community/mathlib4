/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, S√©bastien Gou√´zel
-/
import Mathlib.Order.Bounds.Basic

#align_import order.monotone.union from "leanprover-community/mathlib"@"d012cd09a9b256d870751284dd6a29882b0be105"

/-!
# Monotonicity on intervals

In this file we prove that a function is (strictly) monotone (or antitone) on a linear order `Œ±`
provided that it is (strictly) monotone on `(-‚àû, a]` and on `[a, +‚àû)`. This is a special case
of a more general statement where one deduces monotonicity on a union from monotonicity on each
set.
-/


open Set

variable {Œ± Œ≤ : Type*} [LinearOrder Œ±] [Preorder Œ≤] {a : Œ±} {f : Œ± ‚Üí Œ≤}

/-- If `f` is strictly monotone both on `s` and `t`, with `s` to the left of `t` and the center
point belonging to both `s` and `t`, then `f` is strictly monotone on `s ‚à™ t` -/
protected theorem StrictMonoOn.union {s t : Set Œ±} {c : Œ±} (h‚ÇÅ : StrictMonoOn f s)
    (h‚ÇÇ : StrictMonoOn f t) (hs : IsGreatest s c) (ht : IsLeast t c) : StrictMonoOn f (s ‚à™ t) := by
  have A : ‚àÄ x, x ‚àà s ‚à™ t ‚Üí x ‚â§ c ‚Üí x ‚àà s := by
    intro x hx hxc
    cases hx
    ¬∑ assumption
    rcases eq_or_lt_of_le hxc with (rfl | h'x)
    ¬∑ exact hs.1
    exact (lt_irrefl _ (h'x.trans_le (ht.2 (by assumption)))).elim
  have B : ‚àÄ x, x ‚àà s ‚à™ t ‚Üí c ‚â§ x ‚Üí x ‚àà t := by
    intro x hx hxc
    match hx with
    | Or.inr hx => exact hx
    | Or.inl hx =>
      rcases eq_or_lt_of_le hxc with (rfl | h'x)
      ¬∑ exact ht.1
      exact (lt_irrefl _ (h'x.trans_le (hs.2 hx))).elim
  intro x hx y hy hxy
  -- ‚ä¢ f x < f y
  rcases lt_or_le x c with (hxc | hcx)
  -- ‚ä¢ f x < f y
  ¬∑ have xs : x ‚àà s := A _ hx hxc.le
    -- ‚ä¢ f x < f y
    rcases lt_or_le y c with (hyc | hcy)
    -- ‚ä¢ f x < f y
    ¬∑ exact h‚ÇÅ xs (A _ hy hyc.le) hxy
      -- üéâ no goals
    ¬∑ exact (h‚ÇÅ xs hs.1 hxc).trans_le (h‚ÇÇ.monotoneOn ht.1 (B _ hy hcy) hcy)
      -- üéâ no goals
  ¬∑ have xt : x ‚àà t := B _ hx hcx
    -- ‚ä¢ f x < f y
    have yt : y ‚àà t := B _ hy (hcx.trans hxy.le)
    -- ‚ä¢ f x < f y
    exact h‚ÇÇ xt yt hxy
    -- üéâ no goals
#align strict_mono_on.union StrictMonoOn.union

/-- If `f` is strictly monotone both on `(-‚àû, a]` and `[a, ‚àû)`, then it is strictly monotone on the
whole line. -/
protected theorem StrictMonoOn.Iic_union_Ici (h‚ÇÅ : StrictMonoOn f (Iic a))
    (h‚ÇÇ : StrictMonoOn f (Ici a)) : StrictMono f := by
  rw [‚Üê strictMonoOn_univ, ‚Üê @Iic_union_Ici _ _ a]
  -- ‚ä¢ StrictMonoOn f (Iic a ‚à™ Ici a)
  exact StrictMonoOn.union h‚ÇÅ h‚ÇÇ isGreatest_Iic isLeast_Ici
  -- üéâ no goals
#align strict_mono_on.Iic_union_Ici StrictMonoOn.Iic_union_Ici

/-- If `f` is strictly antitone both on `s` and `t`, with `s` to the left of `t` and the center
point belonging to both `s` and `t`, then `f` is strictly antitone on `s ‚à™ t` -/
protected theorem StrictAntiOn.union {s t : Set Œ±} {c : Œ±} (h‚ÇÅ : StrictAntiOn f s)
    (h‚ÇÇ : StrictAntiOn f t) (hs : IsGreatest s c) (ht : IsLeast t c) : StrictAntiOn f (s ‚à™ t) :=
  (h‚ÇÅ.dual_right.union h‚ÇÇ.dual_right hs ht).dual_right
#align strict_anti_on.union StrictAntiOn.union

/-- If `f` is strictly antitone both on `(-‚àû, a]` and `[a, ‚àû)`, then it is strictly antitone on the
whole line. -/
protected theorem StrictAntiOn.Iic_union_Ici (h‚ÇÅ : StrictAntiOn f (Iic a))
    (h‚ÇÇ : StrictAntiOn f (Ici a)) : StrictAnti f :=
  (h‚ÇÅ.dual_right.Iic_union_Ici h‚ÇÇ.dual_right).dual_right
#align strict_anti_on.Iic_union_Ici StrictAntiOn.Iic_union_Ici

/-- If `f` is monotone both on `s` and `t`, with `s` to the left of `t` and the center
point belonging to both `s` and `t`, then `f` is monotone on `s ‚à™ t` -/
protected theorem MonotoneOn.union_right {s t : Set Œ±} {c : Œ±} (h‚ÇÅ : MonotoneOn f s)
    (h‚ÇÇ : MonotoneOn f t) (hs : IsGreatest s c) (ht : IsLeast t c) : MonotoneOn f (s ‚à™ t) := by
  have A : ‚àÄ x, x ‚àà s ‚à™ t ‚Üí x ‚â§ c ‚Üí x ‚àà s := by
    intro x hx hxc
    cases hx
    ¬∑ assumption
    rcases eq_or_lt_of_le hxc with (rfl | h'x)
    ¬∑ exact hs.1
    exact (lt_irrefl _ (h'x.trans_le (ht.2 (by assumption)))).elim
  have B : ‚àÄ x, x ‚àà s ‚à™ t ‚Üí c ‚â§ x ‚Üí x ‚àà t := by
    intro x hx hxc
    match hx with
    | Or.inr hx => exact hx
    | Or.inl hx =>
      rcases eq_or_lt_of_le hxc with (rfl | h'x)
      ¬∑ exact ht.1
      exact (lt_irrefl _ (h'x.trans_le (hs.2 hx))).elim
  intro x hx y hy hxy
  -- ‚ä¢ f x ‚â§ f y
  rcases lt_or_le x c with (hxc | hcx)
  -- ‚ä¢ f x ‚â§ f y
  ¬∑ have xs : x ‚àà s := A _ hx hxc.le
    -- ‚ä¢ f x ‚â§ f y
    rcases lt_or_le y c with (hyc | hcy)
    -- ‚ä¢ f x ‚â§ f y
    ¬∑ exact h‚ÇÅ xs (A _ hy hyc.le) hxy
      -- üéâ no goals
    ¬∑ exact (h‚ÇÅ xs hs.1 hxc.le).trans (h‚ÇÇ ht.1 (B _ hy hcy) hcy)
      -- üéâ no goals
  ¬∑ have xt : x ‚àà t := B _ hx hcx
    -- ‚ä¢ f x ‚â§ f y
    have yt : y ‚àà t := B _ hy (hcx.trans hxy)
    -- ‚ä¢ f x ‚â§ f y
    exact h‚ÇÇ xt yt hxy
    -- üéâ no goals
#align monotone_on.union_right MonotoneOn.union_right

/-- If `f` is monotone both on `(-‚àû, a]` and `[a, ‚àû)`, then it is monotone on the whole line. -/
protected theorem MonotoneOn.Iic_union_Ici (h‚ÇÅ : MonotoneOn f (Iic a)) (h‚ÇÇ : MonotoneOn f (Ici a)) :
    Monotone f := by
  rw [‚Üê monotoneOn_univ, ‚Üê @Iic_union_Ici _ _ a]
  -- ‚ä¢ MonotoneOn f (Iic a ‚à™ Ici a)
  exact MonotoneOn.union_right h‚ÇÅ h‚ÇÇ isGreatest_Iic isLeast_Ici
  -- üéâ no goals
#align monotone_on.Iic_union_Ici MonotoneOn.Iic_union_Ici

/-- If `f` is antitone both on `s` and `t`, with `s` to the left of `t` and the center
point belonging to both `s` and `t`, then `f` is antitone on `s ‚à™ t` -/
protected theorem AntitoneOn.union_right {s t : Set Œ±} {c : Œ±} (h‚ÇÅ : AntitoneOn f s)
    (h‚ÇÇ : AntitoneOn f t) (hs : IsGreatest s c) (ht : IsLeast t c) : AntitoneOn f (s ‚à™ t) :=
  (h‚ÇÅ.dual_right.union_right h‚ÇÇ.dual_right hs ht).dual_right
#align antitone_on.union_right AntitoneOn.union_right

/-- If `f` is antitone both on `(-‚àû, a]` and `[a, ‚àû)`, then it is antitone on the whole line. -/
protected theorem AntitoneOn.Iic_union_Ici (h‚ÇÅ : AntitoneOn f (Iic a)) (h‚ÇÇ : AntitoneOn f (Ici a)) :
    Antitone f :=
  (h‚ÇÅ.dual_right.Iic_union_Ici h‚ÇÇ.dual_right).dual_right
#align antitone_on.Iic_union_Ici AntitoneOn.Iic_union_Ici
