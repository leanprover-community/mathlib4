/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl
-/
import Mathlib.Order.BoundedOrder

#align_import order.disjoint from "leanprover-community/mathlib"@"22c4d2ff43714b6ff724b2745ccfdc0f236a4a76"

/-!
# Disjointness and complements

This file defines `Disjoint`, `Codisjoint`, and the `IsCompl` predicate.

## Main declarations

* `Disjoint x y`: two elements of a lattice are disjoint if their `inf` is the bottom element.
* `Codisjoint x y`: two elements of a lattice are codisjoint if their `join` is the top element.
* `IsCompl x y`: In a bounded lattice, predicate for "`x` is a complement of `y`". Note that in a
  non distributive lattice, an element can have several complements.
* `ComplementedLattice Œ±`: Typeclass stating that any element of a lattice has a complement.

-/

open Function

variable {Œ± : Type*}

section Disjoint

section PartialOrderBot

variable [PartialOrder Œ±] [OrderBot Œ±] {a b c d : Œ±}

/-- Two elements of a lattice are disjoint if their inf is the bottom element.
  (This generalizes disjoint sets, viewed as members of the subset lattice.)

Note that we define this without reference to `‚äì`, as this allows us to talk about orders where
the infimum is not unique, or where implementing `Inf` would require additional `Decidable`
arguments. -/
def Disjoint (a b : Œ±) : Prop :=
  ‚àÄ ‚¶Éx‚¶Ñ, x ‚â§ a ‚Üí x ‚â§ b ‚Üí x ‚â§ ‚ä•
#align disjoint Disjoint

theorem disjoint_comm : Disjoint a b ‚Üî Disjoint b a :=
  forall_congr' fun _ ‚Ü¶ forall_swap
#align disjoint.comm disjoint_comm

@[symm]
theorem Disjoint.symm ‚¶Éa b : Œ±‚¶Ñ : Disjoint a b ‚Üí Disjoint b a :=
  disjoint_comm.1
#align disjoint.symm Disjoint.symm

theorem symmetric_disjoint : Symmetric (Disjoint : Œ± ‚Üí Œ± ‚Üí Prop) :=
  Disjoint.symm
#align symmetric_disjoint symmetric_disjoint

@[simp]
theorem disjoint_bot_left : Disjoint ‚ä• a := fun _ hbot _ ‚Ü¶ hbot
#align disjoint_bot_left disjoint_bot_left

@[simp]
theorem disjoint_bot_right : Disjoint a ‚ä• := fun _ _ hbot ‚Ü¶ hbot
#align disjoint_bot_right disjoint_bot_right

theorem Disjoint.mono (h‚ÇÅ : a ‚â§ b) (h‚ÇÇ : c ‚â§ d) : Disjoint b d ‚Üí Disjoint a c :=
  fun h _ ha hc ‚Ü¶ h (ha.trans h‚ÇÅ) (hc.trans h‚ÇÇ)
#align disjoint.mono Disjoint.mono

theorem Disjoint.mono_left (h : a ‚â§ b) : Disjoint b c ‚Üí Disjoint a c :=
  Disjoint.mono h le_rfl
#align disjoint.mono_left Disjoint.mono_left

theorem Disjoint.mono_right : b ‚â§ c ‚Üí Disjoint a c ‚Üí Disjoint a b :=
  Disjoint.mono le_rfl
#align disjoint.mono_right Disjoint.mono_right

@[simp]
theorem disjoint_self : Disjoint a a ‚Üî a = ‚ä• :=
  ‚ü®fun hd ‚Ü¶ bot_unique <| hd le_rfl le_rfl, fun h _ ha _ ‚Ü¶ ha.trans_eq h‚ü©
#align disjoint_self disjoint_self

/- TODO: Rename `Disjoint.eq_bot` to `Disjoint.inf_eq` and `Disjoint.eq_bot_of_self` to
`Disjoint.eq_bot` -/
alias ‚ü®Disjoint.eq_bot_of_self, _‚ü© := disjoint_self
#align disjoint.eq_bot_of_self Disjoint.eq_bot_of_self

theorem Disjoint.ne (ha : a ‚â† ‚ä•) (hab : Disjoint a b) : a ‚â† b :=
  fun h ‚Ü¶ ha <| disjoint_self.1 <| by rwa [‚Üê h] at hab
                                      -- üéâ no goals
#align disjoint.ne Disjoint.ne

theorem Disjoint.eq_bot_of_le (hab : Disjoint a b) (h : a ‚â§ b) : a = ‚ä• :=
  eq_bot_iff.2 <| hab le_rfl h
#align disjoint.eq_bot_of_le Disjoint.eq_bot_of_le

theorem Disjoint.eq_bot_of_ge (hab : Disjoint a b) : b ‚â§ a ‚Üí b = ‚ä• :=
  hab.symm.eq_bot_of_le
#align disjoint.eq_bot_of_ge Disjoint.eq_bot_of_ge

end PartialOrderBot

section PartialBoundedOrder

variable [PartialOrder Œ±] [BoundedOrder Œ±] {a : Œ±}

@[simp]
theorem disjoint_top : Disjoint a ‚ä§ ‚Üî a = ‚ä• :=
  ‚ü®fun h ‚Ü¶ bot_unique <| h le_rfl le_top, fun h _ ha _ ‚Ü¶ ha.trans_eq h‚ü©
#align disjoint_top disjoint_top

@[simp]
theorem top_disjoint : Disjoint ‚ä§ a ‚Üî a = ‚ä• :=
  ‚ü®fun h ‚Ü¶ bot_unique <| h le_top le_rfl, fun h _ _ ha ‚Ü¶ ha.trans_eq h‚ü©
#align top_disjoint top_disjoint

end PartialBoundedOrder

section SemilatticeInfBot

variable [SemilatticeInf Œ±] [OrderBot Œ±] {a b c d : Œ±}

theorem disjoint_iff_inf_le : Disjoint a b ‚Üî a ‚äì b ‚â§ ‚ä• :=
  ‚ü®fun hd ‚Ü¶ hd inf_le_left inf_le_right, fun h _ ha hb ‚Ü¶ (le_inf ha hb).trans h‚ü©
#align disjoint_iff_inf_le disjoint_iff_inf_le

theorem disjoint_iff : Disjoint a b ‚Üî a ‚äì b = ‚ä• :=
  disjoint_iff_inf_le.trans le_bot_iff
#align disjoint_iff disjoint_iff

theorem Disjoint.le_bot : Disjoint a b ‚Üí a ‚äì b ‚â§ ‚ä• :=
  disjoint_iff_inf_le.mp
#align disjoint.le_bot Disjoint.le_bot

theorem Disjoint.eq_bot : Disjoint a b ‚Üí a ‚äì b = ‚ä• :=
  bot_unique ‚àò Disjoint.le_bot
#align disjoint.eq_bot Disjoint.eq_bot

theorem disjoint_assoc : Disjoint (a ‚äì b) c ‚Üî Disjoint a (b ‚äì c) := by
  rw [disjoint_iff_inf_le, disjoint_iff_inf_le, inf_assoc]
  -- üéâ no goals
#align disjoint_assoc disjoint_assoc

theorem disjoint_left_comm : Disjoint a (b ‚äì c) ‚Üî Disjoint b (a ‚äì c) := by
  simp_rw [disjoint_iff_inf_le, inf_left_comm]
  -- üéâ no goals
#align disjoint_left_comm disjoint_left_comm

theorem disjoint_right_comm : Disjoint (a ‚äì b) c ‚Üî Disjoint (a ‚äì c) b := by
  simp_rw [disjoint_iff_inf_le, inf_right_comm]
  -- üéâ no goals
#align disjoint_right_comm disjoint_right_comm

variable (c)

theorem Disjoint.inf_left (h : Disjoint a b) : Disjoint (a ‚äì c) b :=
  h.mono_left inf_le_left
#align disjoint.inf_left Disjoint.inf_left

theorem Disjoint.inf_left' (h : Disjoint a b) : Disjoint (c ‚äì a) b :=
  h.mono_left inf_le_right
#align disjoint.inf_left' Disjoint.inf_left'

theorem Disjoint.inf_right (h : Disjoint a b) : Disjoint a (b ‚äì c) :=
  h.mono_right inf_le_left
#align disjoint.inf_right Disjoint.inf_right

theorem Disjoint.inf_right' (h : Disjoint a b) : Disjoint a (c ‚äì b) :=
  h.mono_right inf_le_right
#align disjoint.inf_right' Disjoint.inf_right'

variable {c}

theorem Disjoint.of_disjoint_inf_of_le (h : Disjoint (a ‚äì b) c) (hle : a ‚â§ c) : Disjoint a b :=
  disjoint_iff.2 <| h.eq_bot_of_le <| inf_le_of_left_le hle
#align disjoint.of_disjoint_inf_of_le Disjoint.of_disjoint_inf_of_le

theorem Disjoint.of_disjoint_inf_of_le' (h : Disjoint (a ‚äì b) c) (hle : b ‚â§ c) : Disjoint a b :=
  disjoint_iff.2 <| h.eq_bot_of_le <| inf_le_of_right_le hle
#align disjoint.of_disjoint_inf_of_le' Disjoint.of_disjoint_inf_of_le'

end SemilatticeInfBot

section DistribLatticeBot

variable [DistribLattice Œ±] [OrderBot Œ±] {a b c : Œ±}

@[simp]
theorem disjoint_sup_left : Disjoint (a ‚äî b) c ‚Üî Disjoint a c ‚àß Disjoint b c := by
  simp only [disjoint_iff, inf_sup_right, sup_eq_bot_iff]
  -- üéâ no goals
#align disjoint_sup_left disjoint_sup_left

@[simp]
theorem disjoint_sup_right : Disjoint a (b ‚äî c) ‚Üî Disjoint a b ‚àß Disjoint a c := by
  simp only [disjoint_iff, inf_sup_left, sup_eq_bot_iff]
  -- üéâ no goals
#align disjoint_sup_right disjoint_sup_right

theorem Disjoint.sup_left (ha : Disjoint a c) (hb : Disjoint b c) : Disjoint (a ‚äî b) c :=
  disjoint_sup_left.2 ‚ü®ha, hb‚ü©
#align disjoint.sup_left Disjoint.sup_left

theorem Disjoint.sup_right (hb : Disjoint a b) (hc : Disjoint a c) : Disjoint a (b ‚äî c) :=
  disjoint_sup_right.2 ‚ü®hb, hc‚ü©
#align disjoint.sup_right Disjoint.sup_right

theorem Disjoint.left_le_of_le_sup_right (h : a ‚â§ b ‚äî c) (hd : Disjoint a c) : a ‚â§ b :=
  le_of_inf_le_sup_le (le_trans hd.le_bot bot_le) <| sup_le h le_sup_right
#align disjoint.left_le_of_le_sup_right Disjoint.left_le_of_le_sup_right

theorem Disjoint.left_le_of_le_sup_left (h : a ‚â§ c ‚äî b) (hd : Disjoint a c) : a ‚â§ b :=
  hd.left_le_of_le_sup_right <| by rwa [sup_comm]
                                   -- üéâ no goals
#align disjoint.left_le_of_le_sup_left Disjoint.left_le_of_le_sup_left

end DistribLatticeBot

end Disjoint

section Codisjoint

section PartialOrderTop

variable [PartialOrder Œ±] [OrderTop Œ±] {a b c d : Œ±}

/-- Two elements of a lattice are codisjoint if their sup is the top element.

Note that we define this without reference to `‚äî`, as this allows us to talk about orders where
the supremum is not unique, or where implement `Sup` would require additional `Decidable`
arguments. -/
def Codisjoint (a b : Œ±) : Prop :=
  ‚àÄ ‚¶Éx‚¶Ñ, a ‚â§ x ‚Üí b ‚â§ x ‚Üí ‚ä§ ‚â§ x
#align codisjoint Codisjoint

theorem Codisjoint_comm : Codisjoint a b ‚Üî Codisjoint b a :=
  forall_congr' fun _ ‚Ü¶ forall_swap
#align codisjoint.comm Codisjoint_comm

@[symm]
theorem Codisjoint.symm ‚¶Éa b : Œ±‚¶Ñ : Codisjoint a b ‚Üí Codisjoint b a :=
  Codisjoint_comm.1
#align codisjoint.symm Codisjoint.symm

theorem symmetric_codisjoint : Symmetric (Codisjoint : Œ± ‚Üí Œ± ‚Üí Prop) :=
  Codisjoint.symm
#align symmetric_codisjoint symmetric_codisjoint

@[simp]
theorem codisjoint_top_left : Codisjoint ‚ä§ a := fun _ htop _ ‚Ü¶ htop
#align codisjoint_top_left codisjoint_top_left

@[simp]
theorem codisjoint_top_right : Codisjoint a ‚ä§ := fun _ _ htop ‚Ü¶ htop
#align codisjoint_top_right codisjoint_top_right

theorem Codisjoint.mono (h‚ÇÅ : a ‚â§ b) (h‚ÇÇ : c ‚â§ d) : Codisjoint a c ‚Üí Codisjoint b d :=
  fun h _ ha hc ‚Ü¶ h (h‚ÇÅ.trans ha) (h‚ÇÇ.trans hc)
#align codisjoint.mono Codisjoint.mono

theorem Codisjoint.mono_left (h : a ‚â§ b) : Codisjoint a c ‚Üí Codisjoint b c :=
  Codisjoint.mono h le_rfl
#align codisjoint.mono_left Codisjoint.mono_left

theorem Codisjoint.mono_right : b ‚â§ c ‚Üí Codisjoint a b ‚Üí Codisjoint a c :=
  Codisjoint.mono le_rfl
#align codisjoint.mono_right Codisjoint.mono_right

@[simp]
theorem codisjoint_self : Codisjoint a a ‚Üî a = ‚ä§ :=
  ‚ü®fun hd ‚Ü¶ top_unique <| hd le_rfl le_rfl, fun h _ ha _ ‚Ü¶ h.symm.trans_le ha‚ü©
#align codisjoint_self codisjoint_self

/- TODO: Rename `Codisjoint.eq_top` to `Codisjoint.sup_eq` and `Codisjoint.eq_top_of_self` to
`Codisjoint.eq_top` -/
alias ‚ü®Codisjoint.eq_top_of_self, _‚ü© := codisjoint_self
#align codisjoint.eq_top_of_self Codisjoint.eq_top_of_self

theorem Codisjoint.ne (ha : a ‚â† ‚ä§) (hab : Codisjoint a b) : a ‚â† b :=
  fun h ‚Ü¶ ha <| codisjoint_self.1 <| by rwa [‚Üê h] at hab
                                        -- üéâ no goals
#align codisjoint.ne Codisjoint.ne

theorem Codisjoint.eq_top_of_le (hab : Codisjoint a b) (h : b ‚â§ a) : a = ‚ä§ :=
  eq_top_iff.2 <| hab le_rfl h
#align codisjoint.eq_top_of_le Codisjoint.eq_top_of_le

theorem Codisjoint.eq_top_of_ge (hab : Codisjoint a b) : a ‚â§ b ‚Üí b = ‚ä§ :=
  hab.symm.eq_top_of_le
#align codisjoint.eq_top_of_ge Codisjoint.eq_top_of_ge

end PartialOrderTop

section PartialBoundedOrder

variable [PartialOrder Œ±] [BoundedOrder Œ±] {a : Œ±}

@[simp]
theorem codisjoint_bot : Codisjoint a ‚ä• ‚Üî a = ‚ä§ :=
  ‚ü®fun h ‚Ü¶ top_unique <| h le_rfl bot_le, fun h _ ha _ ‚Ü¶ h.symm.trans_le ha‚ü©
#align codisjoint_bot codisjoint_bot

@[simp]
theorem bot_codisjoint : Codisjoint ‚ä• a ‚Üî a = ‚ä§ :=
  ‚ü®fun h ‚Ü¶ top_unique <| h bot_le le_rfl, fun h _ _ ha ‚Ü¶ h.symm.trans_le ha‚ü©
#align bot_codisjoint bot_codisjoint

end PartialBoundedOrder

section SemilatticeSupTop

variable [SemilatticeSup Œ±] [OrderTop Œ±] {a b c d : Œ±}

theorem codisjoint_iff_le_sup : Codisjoint a b ‚Üî ‚ä§ ‚â§ a ‚äî b :=
  @disjoint_iff_inf_le Œ±·µí·µà _ _ _ _
#align codisjoint_iff_le_sup codisjoint_iff_le_sup

theorem codisjoint_iff : Codisjoint a b ‚Üî a ‚äî b = ‚ä§ :=
  @disjoint_iff Œ±·µí·µà _ _ _ _
#align codisjoint_iff codisjoint_iff

theorem Codisjoint.top_le : Codisjoint a b ‚Üí ‚ä§ ‚â§ a ‚äî b :=
  @Disjoint.le_bot Œ±·µí·µà _ _ _ _
#align codisjoint.top_le Codisjoint.top_le

theorem Codisjoint.eq_top : Codisjoint a b ‚Üí a ‚äî b = ‚ä§ :=
  @Disjoint.eq_bot Œ±·µí·µà _ _ _ _
#align codisjoint.eq_top Codisjoint.eq_top

theorem codisjoint_assoc : Codisjoint (a ‚äî b) c ‚Üî Codisjoint a (b ‚äî c) :=
  @disjoint_assoc Œ±·µí·µà _ _ _ _ _
#align codisjoint_assoc codisjoint_assoc

theorem codisjoint_left_comm : Codisjoint a (b ‚äî c) ‚Üî Codisjoint b (a ‚äî c) :=
  @disjoint_left_comm Œ±·µí·µà _ _ _ _ _
#align codisjoint_left_comm codisjoint_left_comm

theorem codisjoint_right_comm : Codisjoint (a ‚äî b) c ‚Üî Codisjoint (a ‚äî c) b :=
  @disjoint_right_comm Œ±·µí·µà _ _ _ _ _
#align codisjoint_right_comm codisjoint_right_comm

variable (c)

theorem Codisjoint.sup_left (h : Codisjoint a b) : Codisjoint (a ‚äî c) b :=
  h.mono_left le_sup_left
#align codisjoint.sup_left Codisjoint.sup_left

theorem Codisjoint.sup_left' (h : Codisjoint a b) : Codisjoint (c ‚äî a) b :=
  h.mono_left le_sup_right
#align codisjoint.sup_left' Codisjoint.sup_left'

theorem Codisjoint.sup_right (h : Codisjoint a b) : Codisjoint a (b ‚äî c) :=
  h.mono_right le_sup_left
#align codisjoint.sup_right Codisjoint.sup_right

theorem Codisjoint.sup_right' (h : Codisjoint a b) : Codisjoint a (c ‚äî b) :=
  h.mono_right le_sup_right
#align codisjoint.sup_right' Codisjoint.sup_right'

variable {c}

theorem Codisjoint.of_codisjoint_sup_of_le (h : Codisjoint (a ‚äî b) c) (hle : c ‚â§ a) :
    Codisjoint a b :=
  @Disjoint.of_disjoint_inf_of_le Œ±·µí·µà _ _ _ _ _ h hle
#align codisjoint.of_codisjoint_sup_of_le Codisjoint.of_codisjoint_sup_of_le

theorem Codisjoint.of_codisjoint_sup_of_le' (h : Codisjoint (a ‚äî b) c) (hle : c ‚â§ b) :
    Codisjoint a b :=
  @Disjoint.of_disjoint_inf_of_le' Œ±·µí·µà _ _ _ _ _ h hle
#align codisjoint.of_codisjoint_sup_of_le' Codisjoint.of_codisjoint_sup_of_le'

end SemilatticeSupTop

section DistribLatticeTop

variable [DistribLattice Œ±] [OrderTop Œ±] {a b c : Œ±}

@[simp]
theorem codisjoint_inf_left : Codisjoint (a ‚äì b) c ‚Üî Codisjoint a c ‚àß Codisjoint b c := by
  simp only [codisjoint_iff, sup_inf_right, inf_eq_top_iff]
  -- üéâ no goals
#align codisjoint_inf_left codisjoint_inf_left

@[simp]
theorem codisjoint_inf_right : Codisjoint a (b ‚äì c) ‚Üî Codisjoint a b ‚àß Codisjoint a c := by
  simp only [codisjoint_iff, sup_inf_left, inf_eq_top_iff]
  -- üéâ no goals
#align codisjoint_inf_right codisjoint_inf_right

theorem Codisjoint.inf_left (ha : Codisjoint a c) (hb : Codisjoint b c) : Codisjoint (a ‚äì b) c :=
  codisjoint_inf_left.2 ‚ü®ha, hb‚ü©
#align codisjoint.inf_left Codisjoint.inf_left

theorem Codisjoint.inf_right (hb : Codisjoint a b) (hc : Codisjoint a c) : Codisjoint a (b ‚äì c) :=
  codisjoint_inf_right.2 ‚ü®hb, hc‚ü©
#align codisjoint.inf_right Codisjoint.inf_right

theorem Codisjoint.left_le_of_le_inf_right (h : a ‚äì b ‚â§ c) (hd : Codisjoint b c) : a ‚â§ c :=
  @Disjoint.left_le_of_le_sup_right Œ±·µí·µà _ _ _ _ _ h hd.symm
#align codisjoint.left_le_of_le_inf_right Codisjoint.left_le_of_le_inf_right

theorem Codisjoint.left_le_of_le_inf_left (h : b ‚äì a ‚â§ c) (hd : Codisjoint b c) : a ‚â§ c :=
  hd.left_le_of_le_inf_right <| by rwa [inf_comm]
                                   -- üéâ no goals
#align codisjoint.left_le_of_le_inf_left Codisjoint.left_le_of_le_inf_left

end DistribLatticeTop

end Codisjoint

open OrderDual

theorem Disjoint.dual [SemilatticeInf Œ±] [OrderBot Œ±] {a b : Œ±} :
    Disjoint a b ‚Üí Codisjoint (toDual a) (toDual b) :=
  id
#align disjoint.dual Disjoint.dual

theorem Codisjoint.dual [SemilatticeSup Œ±] [OrderTop Œ±] {a b : Œ±} :
    Codisjoint a b ‚Üí Disjoint (toDual a) (toDual b) :=
  id
#align codisjoint.dual Codisjoint.dual

@[simp]
theorem disjoint_toDual_iff [SemilatticeSup Œ±] [OrderTop Œ±] {a b : Œ±} :
    Disjoint (toDual a) (toDual b) ‚Üî Codisjoint a b :=
  Iff.rfl
#align disjoint_to_dual_iff disjoint_toDual_iff

@[simp]
theorem disjoint_ofDual_iff [SemilatticeInf Œ±] [OrderBot Œ±] {a b : Œ±·µí·µà} :
    Disjoint (ofDual a) (ofDual b) ‚Üî Codisjoint a b :=
  Iff.rfl
#align disjoint_of_dual_iff disjoint_ofDual_iff

@[simp]
theorem codisjoint_toDual_iff [SemilatticeInf Œ±] [OrderBot Œ±] {a b : Œ±} :
    Codisjoint (toDual a) (toDual b) ‚Üî Disjoint a b :=
  Iff.rfl
#align codisjoint_to_dual_iff codisjoint_toDual_iff

@[simp]
theorem codisjoint_ofDual_iff [SemilatticeSup Œ±] [OrderTop Œ±] {a b : Œ±·µí·µà} :
    Codisjoint (ofDual a) (ofDual b) ‚Üî Disjoint a b :=
  Iff.rfl
#align codisjoint_of_dual_iff codisjoint_ofDual_iff

section DistribLattice

variable [DistribLattice Œ±] [BoundedOrder Œ±] {a b c : Œ±}

theorem Disjoint.le_of_codisjoint (hab : Disjoint a b) (hbc : Codisjoint b c) : a ‚â§ c := by
  rw [‚Üê @inf_top_eq _ _ _ a, ‚Üê @bot_sup_eq _ _ _ c, ‚Üê hab.eq_bot, ‚Üê hbc.eq_top, sup_inf_right]
  -- ‚ä¢ a ‚äì (b ‚äî c) ‚â§ (a ‚äî c) ‚äì (b ‚äî c)
  exact inf_le_inf_right _ le_sup_left
  -- üéâ no goals
#align disjoint.le_of_codisjoint Disjoint.le_of_codisjoint

end DistribLattice

section IsCompl

/-- Two elements `x` and `y` are complements of each other if `x ‚äî y = ‚ä§` and `x ‚äì y = ‚ä•`. -/
structure IsCompl [PartialOrder Œ±] [BoundedOrder Œ±] (x y : Œ±) : Prop where
  /-- If `x` and `y` are to be complementary in an order, they should be disjoint. -/
  protected disjoint : Disjoint x y
  /-- If `x` and `y` are to be complementary in an order, they should be codisjoint. -/
  protected codisjoint : Codisjoint x y
#align is_compl IsCompl

theorem isCompl_iff [PartialOrder Œ±] [BoundedOrder Œ±] {a b : Œ±} :
    IsCompl a b ‚Üî Disjoint a b ‚àß Codisjoint a b :=
  ‚ü®fun h ‚Ü¶ ‚ü®h.1, h.2‚ü©, fun h ‚Ü¶ ‚ü®h.1, h.2‚ü©‚ü©
#align is_compl_iff isCompl_iff

namespace IsCompl

section BoundedPartialOrder

variable [PartialOrder Œ±] [BoundedOrder Œ±] {x y z : Œ±}

@[symm]
protected theorem symm (h : IsCompl x y) : IsCompl y x :=
  ‚ü®h.1.symm, h.2.symm‚ü©
#align is_compl.symm IsCompl.symm

theorem dual (h : IsCompl x y) : IsCompl (toDual x) (toDual y) :=
  ‚ü®h.2, h.1‚ü©
#align is_compl.dual IsCompl.dual

theorem ofDual {a b : Œ±·µí·µà} (h : IsCompl a b) : IsCompl (ofDual a) (ofDual b) :=
  ‚ü®h.2, h.1‚ü©
#align is_compl.of_dual IsCompl.ofDual

end BoundedPartialOrder

section BoundedLattice

variable [Lattice Œ±] [BoundedOrder Œ±] {x y z : Œ±}

theorem of_le (h‚ÇÅ : x ‚äì y ‚â§ ‚ä•) (h‚ÇÇ : ‚ä§ ‚â§ x ‚äî y) : IsCompl x y :=
  ‚ü®disjoint_iff_inf_le.mpr h‚ÇÅ, codisjoint_iff_le_sup.mpr h‚ÇÇ‚ü©
#align is_compl.of_le IsCompl.of_le

theorem of_eq (h‚ÇÅ : x ‚äì y = ‚ä•) (h‚ÇÇ : x ‚äî y = ‚ä§) : IsCompl x y :=
  ‚ü®disjoint_iff.mpr h‚ÇÅ, codisjoint_iff.mpr h‚ÇÇ‚ü©
#align is_compl.of_eq IsCompl.of_eq

theorem inf_eq_bot (h : IsCompl x y) : x ‚äì y = ‚ä• :=
  h.disjoint.eq_bot
#align is_compl.inf_eq_bot IsCompl.inf_eq_bot

theorem sup_eq_top (h : IsCompl x y) : x ‚äî y = ‚ä§ :=
  h.codisjoint.eq_top
#align is_compl.sup_eq_top IsCompl.sup_eq_top

end BoundedLattice

variable [DistribLattice Œ±] [BoundedOrder Œ±] {a b x y z : Œ±}

theorem inf_left_le_of_le_sup_right (h : IsCompl x y) (hle : a ‚â§ b ‚äî y) : a ‚äì x ‚â§ b :=
  calc
    a ‚äì x ‚â§ (b ‚äî y) ‚äì x := inf_le_inf hle le_rfl
    _ = b ‚äì x ‚äî y ‚äì x := inf_sup_right
    _ = b ‚äì x := by rw [h.symm.inf_eq_bot, sup_bot_eq]
                    -- üéâ no goals
    _ ‚â§ b := inf_le_left
#align is_compl.inf_left_le_of_le_sup_right IsCompl.inf_left_le_of_le_sup_right

theorem le_sup_right_iff_inf_left_le {a b} (h : IsCompl x y) : a ‚â§ b ‚äî y ‚Üî a ‚äì x ‚â§ b :=
  ‚ü®h.inf_left_le_of_le_sup_right, h.symm.dual.inf_left_le_of_le_sup_right‚ü©
#align is_compl.le_sup_right_iff_inf_left_le IsCompl.le_sup_right_iff_inf_left_le

theorem inf_left_eq_bot_iff (h : IsCompl y z) : x ‚äì y = ‚ä• ‚Üî x ‚â§ z := by
  rw [‚Üê le_bot_iff, ‚Üê h.le_sup_right_iff_inf_left_le, bot_sup_eq]
  -- üéâ no goals
#align is_compl.inf_left_eq_bot_iff IsCompl.inf_left_eq_bot_iff

theorem inf_right_eq_bot_iff (h : IsCompl y z) : x ‚äì z = ‚ä• ‚Üî x ‚â§ y :=
  h.symm.inf_left_eq_bot_iff
#align is_compl.inf_right_eq_bot_iff IsCompl.inf_right_eq_bot_iff

theorem disjoint_left_iff (h : IsCompl y z) : Disjoint x y ‚Üî x ‚â§ z := by
  rw [disjoint_iff]
  -- ‚ä¢ x ‚äì y = ‚ä• ‚Üî x ‚â§ z
  exact h.inf_left_eq_bot_iff
  -- üéâ no goals
#align is_compl.disjoint_left_iff IsCompl.disjoint_left_iff

theorem disjoint_right_iff (h : IsCompl y z) : Disjoint x z ‚Üî x ‚â§ y :=
  h.symm.disjoint_left_iff
#align is_compl.disjoint_right_iff IsCompl.disjoint_right_iff

theorem le_left_iff (h : IsCompl x y) : z ‚â§ x ‚Üî Disjoint z y :=
  h.disjoint_right_iff.symm
#align is_compl.le_left_iff IsCompl.le_left_iff

theorem le_right_iff (h : IsCompl x y) : z ‚â§ y ‚Üî Disjoint z x :=
  h.symm.le_left_iff
#align is_compl.le_right_iff IsCompl.le_right_iff

theorem left_le_iff (h : IsCompl x y) : x ‚â§ z ‚Üî Codisjoint z y :=
  h.dual.le_left_iff
#align is_compl.left_le_iff IsCompl.left_le_iff

theorem right_le_iff (h : IsCompl x y) : y ‚â§ z ‚Üî Codisjoint z x :=
  h.symm.left_le_iff
#align is_compl.right_le_iff IsCompl.right_le_iff

protected theorem Antitone {x' y'} (h : IsCompl x y) (h' : IsCompl x' y') (hx : x ‚â§ x') : y' ‚â§ y :=
  h'.right_le_iff.2 <| h.symm.codisjoint.mono_right hx
#align is_compl.antitone IsCompl.Antitone

theorem right_unique (hxy : IsCompl x y) (hxz : IsCompl x z) : y = z :=
  le_antisymm (hxz.Antitone hxy <| le_refl x) (hxy.Antitone hxz <| le_refl x)
#align is_compl.right_unique IsCompl.right_unique

theorem left_unique (hxz : IsCompl x z) (hyz : IsCompl y z) : x = y :=
  hxz.symm.right_unique hyz.symm
#align is_compl.left_unique IsCompl.left_unique

theorem sup_inf {x' y'} (h : IsCompl x y) (h' : IsCompl x' y') : IsCompl (x ‚äî x') (y ‚äì y') :=
  of_eq
    (by rw [inf_sup_right, ‚Üê inf_assoc, h.inf_eq_bot, bot_inf_eq, bot_sup_eq, inf_left_comm,
      h'.inf_eq_bot, inf_bot_eq])
    (by rw [sup_inf_left, @sup_comm _ _ x, sup_assoc, h.sup_eq_top, sup_top_eq, top_inf_eq,
      sup_assoc, sup_left_comm, h'.sup_eq_top, sup_top_eq])
#align is_compl.sup_inf IsCompl.sup_inf

theorem inf_sup {x' y'} (h : IsCompl x y) (h' : IsCompl x' y') : IsCompl (x ‚äì x') (y ‚äî y') :=
  (h.symm.sup_inf h'.symm).symm
#align is_compl.inf_sup IsCompl.inf_sup

end IsCompl

namespace Prod

variable {Œ≤ : Type*} [PartialOrder Œ±] [PartialOrder Œ≤]

protected theorem disjoint_iff [OrderBot Œ±] [OrderBot Œ≤] {x y : Œ± √ó Œ≤} :
    Disjoint x y ‚Üî Disjoint x.1 y.1 ‚àß Disjoint x.2 y.2 := by
  constructor
  -- ‚ä¢ Disjoint x y ‚Üí Disjoint x.fst y.fst ‚àß Disjoint x.snd y.snd
  ¬∑ intro h
    -- ‚ä¢ Disjoint x.fst y.fst ‚àß Disjoint x.snd y.snd
    refine' ‚ü®fun a hx hy ‚Ü¶ (@h (a, ‚ä•) ‚ü®hx, _‚ü© ‚ü®hy, _‚ü©).1,
      fun b hx hy ‚Ü¶ (@h (‚ä•, b) ‚ü®_, hx‚ü© ‚ü®_, hy‚ü©).2‚ü©
    all_goals exact bot_le
    -- üéâ no goals
  ¬∑ rintro ‚ü®ha, hb‚ü© z hza hzb
    -- ‚ä¢ z ‚â§ ‚ä•
    refine' ‚ü®ha hza.1 hzb.1, hb hza.2 hzb.2‚ü©
    -- üéâ no goals
#align prod.disjoint_iff Prod.disjoint_iff

protected theorem codisjoint_iff [OrderTop Œ±] [OrderTop Œ≤] {x y : Œ± √ó Œ≤} :
    Codisjoint x y ‚Üî Codisjoint x.1 y.1 ‚àß Codisjoint x.2 y.2 :=
  @Prod.disjoint_iff Œ±·µí·µà Œ≤·µí·µà _ _ _ _ _ _
#align prod.codisjoint_iff Prod.codisjoint_iff

protected theorem isCompl_iff [BoundedOrder Œ±] [BoundedOrder Œ≤] {x y : Œ± √ó Œ≤} :
    IsCompl x y ‚Üî IsCompl x.1 y.1 ‚àß IsCompl x.2 y.2 := by
  simp_rw [isCompl_iff, Prod.disjoint_iff, Prod.codisjoint_iff, and_and_and_comm]
  -- üéâ no goals
#align prod.is_compl_iff Prod.isCompl_iff

end Prod

section

variable [Lattice Œ±] [BoundedOrder Œ±] {a b x : Œ±}

@[simp]
theorem isCompl_toDual_iff : IsCompl (toDual a) (toDual b) ‚Üî IsCompl a b :=
  ‚ü®IsCompl.ofDual, IsCompl.dual‚ü©
#align is_compl_to_dual_iff isCompl_toDual_iff

@[simp]
theorem isCompl_ofDual_iff {a b : Œ±·µí·µà} : IsCompl (ofDual a) (ofDual b) ‚Üî IsCompl a b :=
  ‚ü®IsCompl.dual, IsCompl.ofDual‚ü©
#align is_compl_of_dual_iff isCompl_ofDual_iff

theorem isCompl_bot_top : IsCompl (‚ä• : Œ±) ‚ä§ :=
  IsCompl.of_eq bot_inf_eq sup_top_eq
#align is_compl_bot_top isCompl_bot_top

theorem isCompl_top_bot : IsCompl (‚ä§ : Œ±) ‚ä• :=
  IsCompl.of_eq inf_bot_eq top_sup_eq
#align is_compl_top_bot isCompl_top_bot

theorem eq_top_of_isCompl_bot (h : IsCompl x ‚ä•) : x = ‚ä§ :=
  sup_bot_eq.symm.trans h.sup_eq_top
#align eq_top_of_is_compl_bot eq_top_of_isCompl_bot

theorem eq_top_of_bot_isCompl (h : IsCompl ‚ä• x) : x = ‚ä§ :=
  eq_top_of_isCompl_bot h.symm
#align eq_top_of_bot_is_compl eq_top_of_bot_isCompl

theorem eq_bot_of_isCompl_top (h : IsCompl x ‚ä§) : x = ‚ä• :=
  eq_top_of_isCompl_bot h.dual
#align eq_bot_of_is_compl_top eq_bot_of_isCompl_top

theorem eq_bot_of_top_isCompl (h : IsCompl ‚ä§ x) : x = ‚ä• :=
  eq_top_of_bot_isCompl h.dual
#align eq_bot_of_top_is_compl eq_bot_of_top_isCompl

end

section IsComplemented

section Lattice

variable [Lattice Œ±] [BoundedOrder Œ±]

/-- An element is *complemented* if it has a complement. -/
def IsComplemented (a : Œ±) : Prop :=
  ‚àÉ b, IsCompl a b
#align is_complemented IsComplemented

theorem isComplemented_bot : IsComplemented (‚ä• : Œ±) :=
  ‚ü®‚ä§, isCompl_bot_top‚ü©
#align is_complemented_bot isComplemented_bot

theorem isComplemented_top : IsComplemented (‚ä§ : Œ±) :=
  ‚ü®‚ä•, isCompl_top_bot‚ü©
#align is_complemented_top isComplemented_top

end Lattice

variable [DistribLattice Œ±] [BoundedOrder Œ±] {a b : Œ±}

theorem IsComplemented.sup : IsComplemented a ‚Üí IsComplemented b ‚Üí IsComplemented (a ‚äî b) :=
  fun ‚ü®a', ha‚ü© ‚ü®b', hb‚ü© => ‚ü®a' ‚äì b', ha.sup_inf hb‚ü©
#align is_complemented.sup IsComplemented.sup

theorem IsComplemented.inf : IsComplemented a ‚Üí IsComplemented b ‚Üí IsComplemented (a ‚äì b) :=
  fun ‚ü®a', ha‚ü© ‚ü®b', hb‚ü© => ‚ü®a' ‚äî b', ha.inf_sup hb‚ü©
#align is_complemented.inf IsComplemented.inf

end IsComplemented

/-- A complemented bounded lattice is one where every element has a (not necessarily unique)
complement. -/
class ComplementedLattice (Œ±) [Lattice Œ±] [BoundedOrder Œ±] : Prop where
  /-- In a `ComplementedLattice`, every element admits a complement. -/
  exists_isCompl : ‚àÄ a : Œ±, ‚àÉ b : Œ±, IsCompl a b
#align complemented_lattice ComplementedLattice

export ComplementedLattice (exists_isCompl)

namespace ComplementedLattice

variable [Lattice Œ±] [BoundedOrder Œ±] [ComplementedLattice Œ±]

instance : ComplementedLattice Œ±·µí·µà :=
  ‚ü®fun a ‚Ü¶
    let ‚ü®b, hb‚ü© := exists_isCompl (show Œ± from a)
    ‚ü®b, hb.dual‚ü©‚ü©

end ComplementedLattice

-- TODO: Define as a sublattice?
/-- The sublattice of complemented elements. -/
@[reducible]
def Complementeds (Œ± : Type*) [Lattice Œ±] [BoundedOrder Œ±] : Type _ := {a : Œ± // IsComplemented a}
#align complementeds Complementeds

namespace Complementeds

section Lattice

variable [Lattice Œ±] [BoundedOrder Œ±] {a b : Complementeds Œ±}

instance hasCoeT : CoeTC (Complementeds Œ±) Œ± := ‚ü®Subtype.val‚ü©
#align complementeds.has_coe_t Complementeds.hasCoeT

theorem coe_injective : Injective ((‚Üë) : Complementeds Œ± ‚Üí Œ±) := Subtype.coe_injective
#align complementeds.coe_injective Complementeds.coe_injective

@[simp, norm_cast]
theorem coe_inj : (a : Œ±) = b ‚Üî a = b := Subtype.coe_inj
#align complementeds.coe_inj Complementeds.coe_inj

-- porting note: removing `simp` because `Subtype.coe_le_coe` already proves it
@[norm_cast]
theorem coe_le_coe : (a : Œ±) ‚â§ b ‚Üî a ‚â§ b := by simp
                                               -- üéâ no goals
#align complementeds.coe_le_coe Complementeds.coe_le_coe

-- porting note: removing `simp` because `Subtype.coe_lt_coe` already proves it
@[norm_cast]
theorem coe_lt_coe : (a : Œ±) < b ‚Üî a < b := Iff.rfl
#align complementeds.coe_lt_coe Complementeds.coe_lt_coe

instance : BoundedOrder (Complementeds Œ±) :=
  Subtype.boundedOrder isComplemented_bot isComplemented_top

@[simp, norm_cast]
theorem coe_bot : ((‚ä• : Complementeds Œ±) : Œ±) = ‚ä• := rfl
#align complementeds.coe_bot Complementeds.coe_bot

@[simp, norm_cast]
theorem coe_top : ((‚ä§ : Complementeds Œ±) : Œ±) = ‚ä§ := rfl
#align complementeds.coe_top Complementeds.coe_top

-- porting note: removing `simp` because `Subtype.mk_bot` already proves it
theorem mk_bot : (‚ü®‚ä•, isComplemented_bot‚ü© : Complementeds Œ±) = ‚ä• := rfl
#align complementeds.mk_bot Complementeds.mk_bot

-- porting note: removing `simp` because `Subtype.mk_top` already proves it
theorem mk_top : (‚ü®‚ä§, isComplemented_top‚ü© : Complementeds Œ±) = ‚ä§ := rfl
#align complementeds.mk_top Complementeds.mk_top

instance : Inhabited (Complementeds Œ±) := ‚ü®‚ä•‚ü©

end Lattice

variable [DistribLattice Œ±] [BoundedOrder Œ±] {a b : Complementeds Œ±}

instance : Sup (Complementeds Œ±) :=
  ‚ü®fun a b => ‚ü®a ‚äî b, a.2.sup b.2‚ü©‚ü©

instance : Inf (Complementeds Œ±) :=
  ‚ü®fun a b => ‚ü®a ‚äì b, a.2.inf b.2‚ü©‚ü©

@[simp, norm_cast]
theorem coe_sup (a b : Complementeds Œ±) : ‚Üë(a ‚äî b) = (a : Œ±) ‚äî b := rfl
#align complementeds.coe_sup Complementeds.coe_sup

@[simp, norm_cast]
theorem coe_inf (a b : Complementeds Œ±) : ‚Üë(a ‚äì b) = (a : Œ±) ‚äì b := rfl
#align complementeds.coe_inf Complementeds.coe_inf

@[simp]
theorem mk_sup_mk {a b : Œ±} (ha : IsComplemented a) (hb : IsComplemented b) :
    (‚ü®a, ha‚ü© ‚äî ‚ü®b, hb‚ü© : Complementeds Œ±) = ‚ü®a ‚äî b, ha.sup hb‚ü© := rfl
#align complementeds.mk_sup_mk Complementeds.mk_sup_mk

@[simp]
theorem mk_inf_mk {a b : Œ±} (ha : IsComplemented a) (hb : IsComplemented b) :
    (‚ü®a, ha‚ü© ‚äì ‚ü®b, hb‚ü© : Complementeds Œ±) = ‚ü®a ‚äì b, ha.inf hb‚ü© := rfl
#align complementeds.mk_inf_mk Complementeds.mk_inf_mk

instance : DistribLattice (Complementeds Œ±) :=
  Complementeds.coe_injective.distribLattice _ coe_sup coe_inf

@[simp, norm_cast]
theorem disjoint_coe : Disjoint (a : Œ±) b ‚Üî Disjoint a b := by
  rw [disjoint_iff, disjoint_iff, ‚Üê coe_inf, ‚Üê coe_bot, coe_inj]
  -- üéâ no goals
#align complementeds.disjoint_coe Complementeds.disjoint_coe

@[simp, norm_cast]
theorem codisjoint_coe : Codisjoint (a : Œ±) b ‚Üî Codisjoint a b := by
  rw [codisjoint_iff, codisjoint_iff, ‚Üê coe_sup, ‚Üê coe_top, coe_inj]
  -- üéâ no goals
#align complementeds.codisjoint_coe Complementeds.codisjoint_coe

@[simp, norm_cast]
theorem isCompl_coe : IsCompl (a : Œ±) b ‚Üî IsCompl a b := by
  simp_rw [isCompl_iff, disjoint_coe, codisjoint_coe]
  -- üéâ no goals
#align complementeds.is_compl_coe Complementeds.isCompl_coe

instance : ComplementedLattice (Complementeds Œ±) :=
  ‚ü®fun ‚ü®a, b, h‚ü© => ‚ü®‚ü®b, a, h.symm‚ü©, isCompl_coe.1 h‚ü©‚ü©

end Complementeds
end IsCompl
