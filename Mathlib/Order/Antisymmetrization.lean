/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Order.Hom.Basic
import Mathlib.Logic.Relation

#align_import order.antisymmetrization from "leanprover-community/mathlib"@"3353f661228bd27f632c600cd1a58b874d847c90"

/-!
# Turning a preorder into a partial order

This file allows to make a preorder into a partial order by quotienting out the elements `a`, `b`
such that `a â‰¤ b` and `b â‰¤ a`.

`Antisymmetrization` is a functor from `Preorder` to `PartialOrder`. See `Preorder_to_PartialOrder`.

## Main declarations

* `AntisymmRel`: The antisymmetrization relation. `AntisymmRel r a b` means that `a` and `b` are
  related both ways by `r`.
* `Antisymmetrization Î± r`: The quotient of `Î±` by `AntisymmRel r`. Even when `r` is just a
  preorder, `Antisymmetrization Î±` is a partial order.
-/

/- Porting Notes: There are many changes from `toAntisymmetrization (Â· â‰¤ Â·)` to
`@toAntisymmetrization Î± (Â· â‰¤ Â·) _` -/

open Function OrderDual

variable {Î± Î² : Type*}

section Relation

variable (r : Î± â†’ Î± â†’ Prop)

/-- The antisymmetrization relation. -/
def AntisymmRel (a b : Î±) : Prop :=
  r a b âˆ§ r b a
#align antisymm_rel AntisymmRel

theorem antisymmRel_swap : AntisymmRel (swap r) = AntisymmRel r :=
  funext fun _ => funext fun _ => propext and_comm
#align antisymm_rel_swap antisymmRel_swap

@[refl]
theorem antisymmRel_refl [IsRefl Î± r] (a : Î±) : AntisymmRel r a a :=
  âŸ¨refl _, refl _âŸ©
#align antisymm_rel_refl antisymmRel_refl

variable {r}

@[symm]
theorem AntisymmRel.symm {a b : Î±} : AntisymmRel r a b â†’ AntisymmRel r b a :=
  And.symm
#align antisymm_rel.symm AntisymmRel.symm

@[trans]
theorem AntisymmRel.trans [IsTrans Î± r] {a b c : Î±} (hab : AntisymmRel r a b)
    (hbc : AntisymmRel r b c) : AntisymmRel r a c :=
  âŸ¨_root_.trans hab.1 hbc.1, _root_.trans hbc.2 hab.2âŸ©
#align antisymm_rel.trans AntisymmRel.trans

instance AntisymmRel.decidableRel [DecidableRel r] : DecidableRel (AntisymmRel r) := fun _ _ =>
  instDecidableAnd
#align antisymm_rel.decidable_rel AntisymmRel.decidableRel

@[simp]
theorem antisymmRel_iff_eq [IsRefl Î± r] [IsAntisymm Î± r] {a b : Î±} : AntisymmRel r a b â†” a = b :=
  antisymm_iff
#align antisymm_rel_iff_eq antisymmRel_iff_eq

alias âŸ¨AntisymmRel.eq, _âŸ© := antisymmRel_iff_eq
#align antisymm_rel.eq AntisymmRel.eq

end Relation

section IsPreorder

variable (Î±) (r : Î± â†’ Î± â†’ Prop) [IsPreorder Î± r]

/-- The antisymmetrization relation as an equivalence relation. -/
@[simps]
def AntisymmRel.setoid : Setoid Î± :=
  âŸ¨AntisymmRel r, antisymmRel_refl _, AntisymmRel.symm, AntisymmRel.transâŸ©
#align antisymm_rel.setoid AntisymmRel.setoid
#align antisymm_rel.setoid_r AntisymmRel.setoid_r

/-- The partial order derived from a preorder by making pairwise comparable elements equal. This is
the quotient by `fun a b => a â‰¤ b âˆ§ b â‰¤ a`. -/
def Antisymmetrization : Type _ :=
  Quotient <| AntisymmRel.setoid Î± r
#align antisymmetrization Antisymmetrization

variable {Î±}

/-- Turn an element into its antisymmetrization. -/
def toAntisymmetrization : Î± â†’ Antisymmetrization Î± r :=
  Quotient.mk _
#align to_antisymmetrization toAntisymmetrization

/-- Get a representative from the antisymmetrization. -/
noncomputable def ofAntisymmetrization : Antisymmetrization Î± r â†’ Î± :=
  Quotient.out'
#align of_antisymmetrization ofAntisymmetrization

instance [Inhabited Î±] : Inhabited (Antisymmetrization Î± r) :=
  by unfold Antisymmetrization; infer_instance
     -- âŠ¢ Inhabited (Quotient (AntisymmRel.setoid Î± r))
                                -- ğŸ‰ no goals

@[elab_as_elim]
protected theorem Antisymmetrization.ind {p : Antisymmetrization Î± r â†’ Prop} :
    (âˆ€ a, p <| toAntisymmetrization r a) â†’ âˆ€ q, p q :=
  Quot.ind
#align antisymmetrization.ind Antisymmetrization.ind

@[elab_as_elim]
protected theorem Antisymmetrization.induction_on {p : Antisymmetrization Î± r â†’ Prop}
    (a : Antisymmetrization Î± r) (h : âˆ€ a, p <| toAntisymmetrization r a) : p a :=
  Quotient.inductionOn' a h
#align antisymmetrization.induction_on Antisymmetrization.induction_on

@[simp]
theorem toAntisymmetrization_ofAntisymmetrization (a : Antisymmetrization Î± r) :
    toAntisymmetrization r (ofAntisymmetrization r a) = a :=
  Quotient.out_eq' _
#align to_antisymmetrization_of_antisymmetrization toAntisymmetrization_ofAntisymmetrization

end IsPreorder

section Preorder

variable [Preorder Î±] [Preorder Î²] {a b : Î±}

theorem AntisymmRel.image {a b : Î±} (h : AntisymmRel (Â· â‰¤ Â·) a b) {f : Î± â†’ Î²} (hf : Monotone f) :
    AntisymmRel (Â· â‰¤ Â·) (f a) (f b) :=
  âŸ¨hf h.1, hf h.2âŸ©
#align antisymm_rel.image AntisymmRel.image

instance instPartialOrderAntisymmetrization : PartialOrder (Antisymmetrization Î± (Â· â‰¤ Â·)) where
  le a b :=
    (Quotient.liftOnâ‚‚' a b (Â· â‰¤ Â·)) fun (_ _ _ _ : Î±) hâ‚ hâ‚‚ =>
      propext âŸ¨fun h => hâ‚.2.trans <| h.trans hâ‚‚.1, fun h => hâ‚.1.trans <| h.trans hâ‚‚.2âŸ©
  lt a b :=
    (Quotient.liftOnâ‚‚' a b (Â· < Â·)) fun (_ _ _ _ : Î±) hâ‚ hâ‚‚ =>
      propext âŸ¨fun h => hâ‚.2.trans_lt <| h.trans_le hâ‚‚.1, fun h =>
                hâ‚.1.trans_lt <| h.trans_le hâ‚‚.2âŸ©
  le_refl a := Quotient.inductionOn' a <| le_refl
  le_trans a b c := Quotient.inductionOnâ‚ƒ' a b c fun _ _ _ => le_trans
  lt_iff_le_not_le a b := Quotient.inductionOnâ‚‚' a b fun _ _ => lt_iff_le_not_le
  le_antisymm a b := Quotient.inductionOnâ‚‚' a b fun _ _ hab hba => Quotient.sound' âŸ¨hab, hbaâŸ©

theorem antisymmetrization_fibration :
    Relation.Fibration (Â· < Â·) (Â· < Â·) (@toAntisymmetrization Î± (Â· â‰¤ Â·) _) := by
  rintro a âŸ¨bâŸ© h
  -- âŠ¢ âˆƒ a', (fun x x_1 => x < x_1) a' a âˆ§ toAntisymmetrization (fun x x_1 => x â‰¤ x â€¦
  exact âŸ¨b, h, rflâŸ©
  -- ğŸ‰ no goals
#align antisymmetrization_fibration antisymmetrization_fibration

theorem acc_antisymmetrization_iff : Acc (Â· < Â·)
    (@toAntisymmetrization Î± (Â· â‰¤ Â·) _ a) â†” Acc (Â· < Â·) a :=
  acc_liftOnâ‚‚'_iff
#align acc_antisymmetrization_iff acc_antisymmetrization_iff

theorem wellFounded_antisymmetrization_iff :
    WellFounded (@LT.lt (Antisymmetrization Î± (Â· â‰¤ Â·)) _) â†” WellFounded (@LT.lt Î± _) :=
  wellFounded_liftOnâ‚‚'_iff
#align well_founded_antisymmetrization_iff wellFounded_antisymmetrization_iff

instance [WellFoundedLT Î±] : WellFoundedLT (Antisymmetrization Î± (Â· â‰¤ Â·)) :=
  âŸ¨wellFounded_antisymmetrization_iff.2 IsWellFounded.wfâŸ©

instance [@DecidableRel Î± (Â· â‰¤ Â·)] [@DecidableRel Î± (Â· < Â·)] [IsTotal Î± (Â· â‰¤ Â·)] :
    LinearOrder (Antisymmetrization Î± (Â· â‰¤ Â·)) :=
  { instPartialOrderAntisymmetrization with
    le_total := fun a b => Quotient.inductionOnâ‚‚' a b <| total_of (Â· â‰¤ Â·),
    decidableLE := fun _ _ => show Decidable (Quotient.liftOnâ‚‚' _ _ _ _) from inferInstance,
    decidableLT := fun _ _ => show Decidable (Quotient.liftOnâ‚‚' _ _ _ _) from inferInstance }

@[simp]
theorem toAntisymmetrization_le_toAntisymmetrization_iff :
    @toAntisymmetrization Î± (Â· â‰¤ Â·) _ a â‰¤ @toAntisymmetrization Î± (Â· â‰¤ Â·) _ b â†” a â‰¤ b :=
  Iff.rfl
#align to_antisymmetrization_le_to_antisymmetrization_iff toAntisymmetrization_le_toAntisymmetrization_iff

@[simp]
theorem toAntisymmetrization_lt_toAntisymmetrization_iff :
    @toAntisymmetrization Î± (Â· â‰¤ Â·) _ a < @toAntisymmetrization Î± (Â· â‰¤ Â·) _ b â†” a < b :=
  Iff.rfl
#align to_antisymmetrization_lt_to_antisymmetrization_iff toAntisymmetrization_lt_toAntisymmetrization_iff

@[simp]
theorem ofAntisymmetrization_le_ofAntisymmetrization_iff {a b : Antisymmetrization Î± (Â· â‰¤ Â·)} :
    ofAntisymmetrization (Â· â‰¤ Â·) a â‰¤ ofAntisymmetrization (Â· â‰¤ Â·) b â†” a â‰¤ b :=
  (Quotient.out'RelEmbedding _).map_rel_iff
#align of_antisymmetrization_le_of_antisymmetrization_iff ofAntisymmetrization_le_ofAntisymmetrization_iff

@[simp]
theorem ofAntisymmetrization_lt_ofAntisymmetrization_iff {a b : Antisymmetrization Î± (Â· â‰¤ Â·)} :
    ofAntisymmetrization (Â· â‰¤ Â·) a < ofAntisymmetrization (Â· â‰¤ Â·) b â†” a < b :=
  (Quotient.out'RelEmbedding _).map_rel_iff
#align of_antisymmetrization_lt_of_antisymmetrization_iff ofAntisymmetrization_lt_ofAntisymmetrization_iff

@[mono]
theorem toAntisymmetrization_mono : Monotone (@toAntisymmetrization Î± (Â· â‰¤ Â·) _) := fun _ _ => id
#align to_antisymmetrization_mono toAntisymmetrization_mono

private theorem liftFun_antisymmRel (f : Î± â†’o Î²) :
    ((AntisymmRel.setoid Î± (Â· â‰¤ Â·)).r â‡’ (AntisymmRel.setoid Î² (Â· â‰¤ Â·)).r) f f := fun _ _ h =>
  âŸ¨f.mono h.1, f.mono h.2âŸ©

/-- Turns an order homomorphism from `Î±` to `Î²` into one from `Antisymmetrization Î±` to
`Antisymmetrization Î²`. `Antisymmetrization` is actually a functor. See `Preorder_to_PartialOrder`.
-/
protected def OrderHom.antisymmetrization (f : Î± â†’o Î²) :
    Antisymmetrization Î± (Â· â‰¤ Â·) â†’o Antisymmetrization Î² (Â· â‰¤ Â·) :=
  âŸ¨Quotient.map' f <| liftFun_antisymmRel f, fun a b => Quotient.inductionOnâ‚‚' a b <| f.monoâŸ©
#align order_hom.antisymmetrization OrderHom.antisymmetrization

@[simp]
theorem OrderHom.coe_antisymmetrization (f : Î± â†’o Î²) :
    â‡‘f.antisymmetrization = Quotient.map' f (liftFun_antisymmRel f) :=
  rfl
#align order_hom.coe_antisymmetrization OrderHom.coe_antisymmetrization

/- Porting notes: Removed @[simp] attribute. With this `simp` lemma the LHS of
`OrderHom.antisymmetrization_apply_mk` is not in normal-form -/
theorem OrderHom.antisymmetrization_apply (f : Î± â†’o Î²) (a : Antisymmetrization Î± (Â· â‰¤ Â·)) :
    f.antisymmetrization a = Quotient.map' f (liftFun_antisymmRel f) a :=
  rfl
#align order_hom.antisymmetrization_apply OrderHom.antisymmetrization_apply

@[simp]
theorem OrderHom.antisymmetrization_apply_mk (f : Î± â†’o Î²) (a : Î±) :
    f.antisymmetrization (toAntisymmetrization _ a) = toAntisymmetrization _ (f a) :=
  @Quotient.map_mk _ _ (_root_.id _) (_root_.id _) f (liftFun_antisymmRel f) _
#align order_hom.antisymmetrization_apply_mk OrderHom.antisymmetrization_apply_mk

variable (Î±)

/-- `ofAntisymmetrization` as an order embedding. -/
@[simps]
noncomputable def OrderEmbedding.ofAntisymmetrization : Antisymmetrization Î± (Â· â‰¤ Â·) â†ªo Î± :=
  { Quotient.out'RelEmbedding _ with toFun := _root_.ofAntisymmetrization _ }
#align order_embedding.of_antisymmetrization OrderEmbedding.ofAntisymmetrization
#align order_embedding.of_antisymmetrization_apply OrderEmbedding.ofAntisymmetrization_apply

/-- `Antisymmetrization` and `orderDual` commute. -/
def OrderIso.dualAntisymmetrization :
    (Antisymmetrization Î± (Â· â‰¤ Â·))áµ’áµˆ â‰ƒo Antisymmetrization Î±áµ’áµˆ (Â· â‰¤ Â·) where
  toFun := (Quotient.map' id) fun _ _ => And.symm
  invFun := (Quotient.map' id) fun _ _ => And.symm
  left_inv a := Quotient.inductionOn' a fun a => by simp_rw [Quotient.map'_mk'', id]
                                                    -- ğŸ‰ no goals
  right_inv a := Quotient.inductionOn' a fun a => by simp_rw [Quotient.map'_mk'', id]
                                                     -- ğŸ‰ no goals
  map_rel_iff' := @fun a b => Quotient.inductionOnâ‚‚' a b fun a b => Iff.rfl
#align order_iso.dual_antisymmetrization OrderIso.dualAntisymmetrization

@[simp]
theorem OrderIso.dualAntisymmetrization_apply (a : Î±) :
    OrderIso.dualAntisymmetrization _ (toDual <| toAntisymmetrization _ a) =
      toAntisymmetrization _ (toDual a) :=
  rfl
#align order_iso.dual_antisymmetrization_apply OrderIso.dualAntisymmetrization_apply

@[simp]
theorem OrderIso.dualAntisymmetrization_symm_apply (a : Î±) :
    (OrderIso.dualAntisymmetrization _).symm (toAntisymmetrization _ <| toDual a) =
      toDual (toAntisymmetrization _ a) :=
  rfl
#align order_iso.dual_antisymmetrization_symm_apply OrderIso.dualAntisymmetrization_symm_apply

end Preorder
