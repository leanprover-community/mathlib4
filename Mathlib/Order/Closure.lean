/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Ya√´l Dillies
-/
import Mathlib.Data.Set.Lattice
import Mathlib.Data.SetLike.Basic
import Mathlib.Order.GaloisConnection
import Mathlib.Order.Hom.Basic

#align_import order.closure from "leanprover-community/mathlib"@"f252872231e87a5db80d9938fc05530e70f23a94"

/-!
# Closure operators between preorders

We define (bundled) closure operators on a preorder as monotone (increasing), extensive
(inflationary) and idempotent functions.
We define closed elements for the operator as elements which are fixed by it.

Lower adjoints to a function between preorders `u : Œ≤ ‚Üí Œ±` allow to generalise closure operators to
situations where the closure operator we are dealing with naturally decomposes as `u ‚àò l` where `l`
is a worthy function to have on its own. Typical examples include
`l : Set G ‚Üí Subgroup G := Subgroup.closure`, `u : Subgroup G ‚Üí Set G := (‚Üë)`, where `G` is a group.
This shows there is a close connection between closure operators, lower adjoints and Galois
connections/insertions: every Galois connection induces a lower adjoint which itself induces a
closure operator by composition (see `GaloisConnection.lowerAdjoint` and
`LowerAdjoint.closureOperator`), and every closure operator on a partial order induces a Galois
insertion from the set of closed elements to the underlying type (see `ClosureOperator.gi`).

## Main definitions

* `ClosureOperator`: A closure operator is a monotone function `f : Œ± ‚Üí Œ±` such that
  `‚àÄ x, x ‚â§ f x` and `‚àÄ x, f (f x) = f x`.
* `LowerAdjoint`: A lower adjoint to `u : Œ≤ ‚Üí Œ±` is a function `l : Œ± ‚Üí Œ≤` such that `l` and `u`
  form a Galois connection.

## Implementation details

Although `LowerAdjoint` is technically a generalisation of `ClosureOperator` (by defining
`toFun := id`), it is desirable to have both as otherwise `id`s would be carried all over the
place when using concrete closure operators such as `ConvexHull`.

`LowerAdjoint` really is a semibundled `structure` version of `GaloisConnection`.

## References

* https://en.wikipedia.org/wiki/Closure_operator#Closure_operators_on_partially_ordered_sets
-/


universe u

/-! ### Closure operator -/


variable (Œ± : Type*) {Œπ : Sort*} {Œ∫ : Œπ ‚Üí Sort*}

/-- A closure operator on the preorder `Œ±` is a monotone function which is extensive (every `x`
is less than its closure) and idempotent. -/
structure ClosureOperator [Preorder Œ±] extends Œ± ‚Üío Œ± where
  /-- An element is less than or equal its closure -/
  le_closure' : ‚àÄ x, x ‚â§ toFun x
  /-- Closures are idempotent -/
  idempotent' : ‚àÄ x, toFun (toFun x) = toFun x
#align closure_operator ClosureOperator

namespace ClosureOperator

instance [Preorder Œ±] : OrderHomClass (ClosureOperator Œ±) Œ± Œ± where
  coe c := c.1
  coe_injective' := by rintro ‚ü®‚ü© ‚ü®‚ü© h; congr; exact FunLike.ext' h
                       -- ‚ä¢ { toOrderHom := toOrderHom‚úù¬π, le_closure' := le_closure'‚úù¬π, idempotent' := i ‚Ä¶
                                       -- ‚ä¢ toOrderHom‚úù¬π = toOrderHom‚úù
                                              -- üéâ no goals
  map_rel f _ _ h := f.mono h

initialize_simps_projections ClosureOperator (toFun ‚Üí apply)

section PartialOrder

variable [PartialOrder Œ±]

/-- The identity function as a closure operator. -/
@[simps!]
def id : ClosureOperator Œ± where
  toOrderHom := OrderHom.id
  le_closure' _ := le_rfl
  idempotent' _ := rfl
#align closure_operator.id ClosureOperator.id
#align closure_operator.id_apply ClosureOperator.id_apply

instance : Inhabited (ClosureOperator Œ±) :=
  ‚ü®id Œ±‚ü©

variable {Œ±} [PartialOrder Œ±] (c : ClosureOperator Œ±)

@[ext]
theorem ext : ‚àÄ c‚ÇÅ c‚ÇÇ : ClosureOperator Œ±, (c‚ÇÅ : Œ± ‚Üí Œ±) = (c‚ÇÇ : Œ± ‚Üí Œ±) ‚Üí c‚ÇÅ = c‚ÇÇ
  | ‚ü®‚ü®c‚ÇÅ, _‚ü©, _, _‚ü©, ‚ü®‚ü®c‚ÇÇ, _‚ü©, _, _‚ü©, h => by
    congr
    -- üéâ no goals
#align closure_operator.ext ClosureOperator.ext

/-- Constructor for a closure operator using the weaker idempotency axiom: `f (f x) ‚â§ f x`. -/
@[simps]
def mk' (f : Œ± ‚Üí Œ±) (hf‚ÇÅ : Monotone f) (hf‚ÇÇ : ‚àÄ x, x ‚â§ f x) (hf‚ÇÉ : ‚àÄ x, f (f x) ‚â§ f x) :
    ClosureOperator Œ± where
  toFun := f
  monotone' := hf‚ÇÅ
  le_closure' := hf‚ÇÇ
  idempotent' x := (hf‚ÇÉ x).antisymm (hf‚ÇÅ (hf‚ÇÇ x))
#align closure_operator.mk' ClosureOperator.mk'
#align closure_operator.mk'_apply ClosureOperator.mk'_apply

/-- Convenience constructor for a closure operator using the weaker minimality axiom:
`x ‚â§ f y ‚Üí f x ‚â§ f y`, which is sometimes easier to prove in practice. -/
@[simps]
def mk‚ÇÇ (f : Œ± ‚Üí Œ±) (hf : ‚àÄ x, x ‚â§ f x) (hmin : ‚àÄ ‚¶Éx y‚¶Ñ, x ‚â§ f y ‚Üí f x ‚â§ f y) : ClosureOperator Œ±
    where
  toFun := f
  monotone' _ y hxy := hmin (hxy.trans (hf y))
  le_closure' := hf
  idempotent' _ := (hmin le_rfl).antisymm (hf _)
#align closure_operator.mk‚ÇÇ ClosureOperator.mk‚ÇÇ
#align closure_operator.mk‚ÇÇ_apply ClosureOperator.mk‚ÇÇ_apply

/-- Expanded out version of `mk‚ÇÇ`. `p` implies being closed. This constructor should be used when
you already know a sufficient condition for being closed and using `mem_mk‚ÇÉ_closed` will avoid you
the (slight) hassle of having to prove it both inside and outside the constructor. -/
@[simps!]
def mk‚ÇÉ (f : Œ± ‚Üí Œ±) (p : Œ± ‚Üí Prop) (hf : ‚àÄ x, x ‚â§ f x) (hfp : ‚àÄ x, p (f x))
    (hmin : ‚àÄ ‚¶Éx y‚¶Ñ, x ‚â§ y ‚Üí p y ‚Üí f x ‚â§ y) : ClosureOperator Œ± :=
  mk‚ÇÇ f hf fun _ y hxy => hmin hxy (hfp y)
#align closure_operator.mk‚ÇÉ ClosureOperator.mk‚ÇÉ
#align closure_operator.mk‚ÇÉ_apply ClosureOperator.mk‚ÇÉ_apply

/-- This lemma shows that the image of `x` of a closure operator built from the `mk‚ÇÉ` constructor
respects `p`, the property that was fed into it. -/
theorem closure_mem_mk‚ÇÉ {f : Œ± ‚Üí Œ±} {p : Œ± ‚Üí Prop} {hf : ‚àÄ x, x ‚â§ f x} {hfp : ‚àÄ x, p (f x)}
    {hmin : ‚àÄ ‚¶Éx y‚¶Ñ, x ‚â§ y ‚Üí p y ‚Üí f x ‚â§ y} (x : Œ±) : p (mk‚ÇÉ f p hf hfp hmin x) :=
  hfp x
#align closure_operator.closure_mem_mk‚ÇÉ ClosureOperator.closure_mem_mk‚ÇÉ

/-- Analogue of `closure_le_closed_iff_le` but with the `p` that was fed into the `mk‚ÇÉ` constructor.
-/
theorem closure_le_mk‚ÇÉ_iff {f : Œ± ‚Üí Œ±} {p : Œ± ‚Üí Prop} {hf : ‚àÄ x, x ‚â§ f x} {hfp : ‚àÄ x, p (f x)}
    {hmin : ‚àÄ ‚¶Éx y‚¶Ñ, x ‚â§ y ‚Üí p y ‚Üí f x ‚â§ y} {x y : Œ±} (hxy : x ‚â§ y) (hy : p y) :
    mk‚ÇÉ f p hf hfp hmin x ‚â§ y :=
  hmin hxy hy
#align closure_operator.closure_le_mk‚ÇÉ_iff ClosureOperator.closure_le_mk‚ÇÉ_iff

@[mono]
theorem monotone : Monotone c :=
  c.monotone'
#align closure_operator.monotone ClosureOperator.monotone

/-- Every element is less than its closure. This property is sometimes referred to as extensivity or
inflationarity. -/
theorem le_closure (x : Œ±) : x ‚â§ c x :=
  c.le_closure' x
#align closure_operator.le_closure ClosureOperator.le_closure

@[simp]
theorem idempotent (x : Œ±) : c (c x) = c x :=
  c.idempotent' x
#align closure_operator.idempotent ClosureOperator.idempotent

theorem le_closure_iff (x y : Œ±) : x ‚â§ c y ‚Üî c x ‚â§ c y :=
  ‚ü®fun h => c.idempotent y ‚ñ∏ c.monotone h, fun h => (c.le_closure x).trans h‚ü©
#align closure_operator.le_closure_iff ClosureOperator.le_closure_iff

/-- An element `x` is closed for the closure operator `c` if it is a fixed point for it. -/
def closed : Set Œ± := {x | c x = x}
#align closure_operator.closed ClosureOperator.closed

theorem mem_closed_iff (x : Œ±) : x ‚àà c.closed ‚Üî c x = x :=
  Iff.rfl
#align closure_operator.mem_closed_iff ClosureOperator.mem_closed_iff

theorem mem_closed_iff_closure_le (x : Œ±) : x ‚àà c.closed ‚Üî c x ‚â§ x :=
  ‚ü®le_of_eq, fun h => h.antisymm (c.le_closure x)‚ü©
#align closure_operator.mem_closed_iff_closure_le ClosureOperator.mem_closed_iff_closure_le

theorem closure_eq_self_of_mem_closed {x : Œ±} (h : x ‚àà c.closed) : c x = x :=
  h
#align closure_operator.closure_eq_self_of_mem_closed ClosureOperator.closure_eq_self_of_mem_closed

@[simp]
theorem closure_is_closed (x : Œ±) : c x ‚àà c.closed :=
  c.idempotent x
#align closure_operator.closure_is_closed ClosureOperator.closure_is_closed

/-- The set of closed elements for `c` is exactly its range. -/
theorem closed_eq_range_close : c.closed = Set.range c :=
  Set.ext fun x =>
    ‚ü®fun h => ‚ü®x, h‚ü©, by
      rintro ‚ü®y, rfl‚ü©
      -- ‚ä¢ ‚Üëc y ‚àà closed c
      apply c.idempotent‚ü©
      -- üéâ no goals
#align closure_operator.closed_eq_range_close ClosureOperator.closed_eq_range_close

/-- Send an `x` to an element of the set of closed elements (by taking the closure). -/
def toClosed (x : Œ±) : c.closed :=
  ‚ü®c x, c.closure_is_closed x‚ü©
#align closure_operator.to_closed ClosureOperator.toClosed

@[simp]
theorem closure_le_closed_iff_le (x : Œ±) {y : Œ±} (hy : c.closed y) : c x ‚â§ y ‚Üî x ‚â§ y := by
  rw [‚Üê c.closure_eq_self_of_mem_closed hy, ‚Üê le_closure_iff]
  -- üéâ no goals
#align closure_operator.closure_le_closed_iff_le ClosureOperator.closure_le_closed_iff_le

/-- A closure operator is equal to the closure operator obtained by feeding `c.closed` into the
`mk‚ÇÉ` constructor. -/
theorem eq_mk‚ÇÉ_closed (c : ClosureOperator Œ±) :
    c =
      mk‚ÇÉ c c.closed c.le_closure c.closure_is_closed fun x y hxy hy =>
        (c.closure_le_closed_iff_le x hy).2 hxy := by
  ext
  -- ‚ä¢ ‚Üëc x‚úù = ‚Üë(mk‚ÇÉ (‚Üëc) (closed c) (_ : ‚àÄ (x : Œ±), x ‚â§ ‚Üëc x) (_ : ‚àÄ (x : Œ±), ‚Üëc x ‚Ä¶
  rfl
  -- üéâ no goals
#align closure_operator.eq_mk‚ÇÉ_closed ClosureOperator.eq_mk‚ÇÉ_closed

/-- The property `p` fed into the `mk‚ÇÉ` constructor implies being closed. -/
theorem mem_mk‚ÇÉ_closed {f : Œ± ‚Üí Œ±} {p : Œ± ‚Üí Prop} {hf : ‚àÄ x, x ‚â§ f x} {hfp : ‚àÄ x, p (f x)}
    {hmin : ‚àÄ ‚¶Éx y‚¶Ñ, x ‚â§ y ‚Üí p y ‚Üí f x ‚â§ y} {x : Œ±} (hx : p x) : x ‚àà (mk‚ÇÉ f p hf hfp hmin).closed :=
  (hmin le_rfl hx).antisymm (hf _)
#align closure_operator.mem_mk‚ÇÉ_closed ClosureOperator.mem_mk‚ÇÉ_closed

end PartialOrder

variable {Œ±}

section OrderTop

variable [PartialOrder Œ±] [OrderTop Œ±] (c : ClosureOperator Œ±)

@[simp]
theorem closure_top : c ‚ä§ = ‚ä§ :=
  le_top.antisymm (c.le_closure _)
#align closure_operator.closure_top ClosureOperator.closure_top

theorem top_mem_closed : ‚ä§ ‚àà c.closed :=
  c.closure_top
#align closure_operator.top_mem_closed ClosureOperator.top_mem_closed

end OrderTop

theorem closure_inf_le [SemilatticeInf Œ±] (c : ClosureOperator Œ±) (x y : Œ±) :
    c (x ‚äì y) ‚â§ c x ‚äì c y :=
  c.monotone.map_inf_le _ _
#align closure_operator.closure_inf_le ClosureOperator.closure_inf_le

section SemilatticeSup

variable [SemilatticeSup Œ±] (c : ClosureOperator Œ±)

theorem closure_sup_closure_le (x y : Œ±) : c x ‚äî c y ‚â§ c (x ‚äî y) :=
  c.monotone.le_map_sup _ _
#align closure_operator.closure_sup_closure_le ClosureOperator.closure_sup_closure_le

theorem closure_sup_closure_left (x y : Œ±) : c (c x ‚äî y) = c (x ‚äî y) :=
  ((c.le_closure_iff _ _).1
        (sup_le (c.monotone le_sup_left) (le_sup_right.trans (c.le_closure _)))).antisymm
    (c.monotone (sup_le_sup_right (c.le_closure _) _))
#align closure_operator.closure_sup_closure_left ClosureOperator.closure_sup_closure_left

theorem closure_sup_closure_right (x y : Œ±) : c (x ‚äî c y) = c (x ‚äî y) := by
  rw [sup_comm, closure_sup_closure_left, sup_comm (a := x)]
  -- üéâ no goals
#align closure_operator.closure_sup_closure_right ClosureOperator.closure_sup_closure_right

theorem closure_sup_closure (x y : Œ±) : c (c x ‚äî c y) = c (x ‚äî y) := by
  rw [closure_sup_closure_left, closure_sup_closure_right]
  -- üéâ no goals
#align closure_operator.closure_sup_closure ClosureOperator.closure_sup_closure

end SemilatticeSup

section CompleteLattice

variable [CompleteLattice Œ±] (c : ClosureOperator Œ±)

@[simp]
theorem closure_iSup_closure (f : Œπ ‚Üí Œ±) : c (‚®Ü i, c (f i)) = c (‚®Ü i, f i) :=
  le_antisymm ((c.le_closure_iff _ _).1 <| iSup_le fun i => c.monotone <| le_iSup f i) <|
    c.monotone <| iSup_mono fun _ => c.le_closure _
#align closure_operator.closure_supr_closure ClosureOperator.closure_iSup_closure

@[simp]
theorem closure_iSup‚ÇÇ_closure (f : ‚àÄ i, Œ∫ i ‚Üí Œ±) :
    c (‚®Ü (i) (j), c (f i j)) = c (‚®Ü (i) (j), f i j) :=
  le_antisymm ((c.le_closure_iff _ _).1 <| iSup‚ÇÇ_le fun i j => c.monotone <| le_iSup‚ÇÇ i j) <|
    c.monotone <| iSup‚ÇÇ_mono fun _ _ => c.le_closure _
#align closure_operator.closure_supr‚ÇÇ_closure ClosureOperator.closure_iSup‚ÇÇ_closure

end CompleteLattice

end ClosureOperator

/-! ### Lower adjoint -/


variable {Œ±} {Œ≤ : Type*}

/-- A lower adjoint of `u` on the preorder `Œ±` is a function `l` such that `l` and `u` form a Galois
connection. It allows us to define closure operators whose output does not match the input. In
practice, `u` is often `(‚Üë) : Œ≤ ‚Üí Œ±`. -/
structure LowerAdjoint [Preorder Œ±] [Preorder Œ≤] (u : Œ≤ ‚Üí Œ±) where
  /-- The underlying function -/
  toFun : Œ± ‚Üí Œ≤
  /-- The underlying function is a lower adjoint. -/
  gc' : GaloisConnection toFun u
#align lower_adjoint LowerAdjoint

namespace LowerAdjoint

variable (Œ±)

/-- The identity function as a lower adjoint to itself. -/
@[simps]
protected def id [Preorder Œ±] : LowerAdjoint (id : Œ± ‚Üí Œ±) where
  toFun x := x
  gc' := GaloisConnection.id
#align lower_adjoint.id LowerAdjoint.id
#align lower_adjoint.id_to_fun LowerAdjoint.id_toFun

variable {Œ±}

instance [Preorder Œ±] : Inhabited (LowerAdjoint (id : Œ± ‚Üí Œ±)) :=
  ‚ü®LowerAdjoint.id Œ±‚ü©

section Preorder

variable [Preorder Œ±] [Preorder Œ≤] {u : Œ≤ ‚Üí Œ±} (l : LowerAdjoint u)

instance : CoeFun (LowerAdjoint u) fun _ => Œ± ‚Üí Œ≤ where coe := toFun

theorem gc : GaloisConnection l u :=
  l.gc'
#align lower_adjoint.gc LowerAdjoint.gc

@[ext]
theorem ext : ‚àÄ l‚ÇÅ l‚ÇÇ : LowerAdjoint u, (l‚ÇÅ : Œ± ‚Üí Œ≤) = (l‚ÇÇ : Œ± ‚Üí Œ≤) ‚Üí l‚ÇÅ = l‚ÇÇ
  | ‚ü®l‚ÇÅ, _‚ü©, ‚ü®l‚ÇÇ, _‚ü©, h => by
    congr
    -- üéâ no goals
#align lower_adjoint.ext LowerAdjoint.ext

@[mono]
theorem monotone : Monotone (u ‚àò l) :=
  l.gc.monotone_u.comp l.gc.monotone_l
#align lower_adjoint.monotone LowerAdjoint.monotone

/-- Every element is less than its closure. This property is sometimes referred to as extensivity or
inflationarity. -/
theorem le_closure (x : Œ±) : x ‚â§ u (l x) :=
  l.gc.le_u_l _
#align lower_adjoint.le_closure LowerAdjoint.le_closure

end Preorder

section PartialOrder

variable [PartialOrder Œ±] [Preorder Œ≤] {u : Œ≤ ‚Üí Œ±} (l : LowerAdjoint u)

/-- Every lower adjoint induces a closure operator given by the composition. This is the partial
order version of the statement that every adjunction induces a monad. -/
@[simps]
def closureOperator : ClosureOperator Œ± where
  toFun x := u (l x)
  monotone' := l.monotone
  le_closure' := l.le_closure
  idempotent' x := l.gc.u_l_u_eq_u (l x)
#align lower_adjoint.closure_operator LowerAdjoint.closureOperator
#align lower_adjoint.closure_operator_apply LowerAdjoint.closureOperator_apply

theorem idempotent (x : Œ±) : u (l (u (l x))) = u (l x) :=
  l.closureOperator.idempotent _
#align lower_adjoint.idempotent LowerAdjoint.idempotent

theorem le_closure_iff (x y : Œ±) : x ‚â§ u (l y) ‚Üî u (l x) ‚â§ u (l y) :=
  l.closureOperator.le_closure_iff _ _
#align lower_adjoint.le_closure_iff LowerAdjoint.le_closure_iff

end PartialOrder

section Preorder

variable [Preorder Œ±] [Preorder Œ≤] {u : Œ≤ ‚Üí Œ±} (l : LowerAdjoint u)

/-- An element `x` is closed for `l : LowerAdjoint u` if it is a fixed point: `u (l x) = x` -/
def closed : Set Œ± := {x | u (l x) = x}
#align lower_adjoint.closed LowerAdjoint.closed

theorem mem_closed_iff (x : Œ±) : x ‚àà l.closed ‚Üî u (l x) = x :=
  Iff.rfl
#align lower_adjoint.mem_closed_iff LowerAdjoint.mem_closed_iff

theorem closure_eq_self_of_mem_closed {x : Œ±} (h : x ‚àà l.closed) : u (l x) = x :=
  h
#align lower_adjoint.closure_eq_self_of_mem_closed LowerAdjoint.closure_eq_self_of_mem_closed

end Preorder

section PartialOrder

variable [PartialOrder Œ±] [PartialOrder Œ≤] {u : Œ≤ ‚Üí Œ±} (l : LowerAdjoint u)

theorem mem_closed_iff_closure_le (x : Œ±) : x ‚àà l.closed ‚Üî u (l x) ‚â§ x :=
  l.closureOperator.mem_closed_iff_closure_le _
#align lower_adjoint.mem_closed_iff_closure_le LowerAdjoint.mem_closed_iff_closure_le

@[simp, nolint simpNF] -- Porting note: lemma does prove itself, seems to be a linter error
theorem closure_is_closed (x : Œ±) : u (l x) ‚àà l.closed :=
  l.idempotent x
#align lower_adjoint.closure_is_closed LowerAdjoint.closure_is_closed

/-- The set of closed elements for `l` is the range of `u ‚àò l`. -/
theorem closed_eq_range_close : l.closed = Set.range (u ‚àò l) :=
  l.closureOperator.closed_eq_range_close
#align lower_adjoint.closed_eq_range_close LowerAdjoint.closed_eq_range_close

/-- Send an `x` to an element of the set of closed elements (by taking the closure). -/
def toClosed (x : Œ±) : l.closed :=
  ‚ü®u (l x), l.closure_is_closed x‚ü©
#align lower_adjoint.to_closed LowerAdjoint.toClosed

@[simp]
theorem closure_le_closed_iff_le (x : Œ±) {y : Œ±} (hy : l.closed y) : u (l x) ‚â§ y ‚Üî x ‚â§ y :=
  l.closureOperator.closure_le_closed_iff_le x hy
#align lower_adjoint.closure_le_closed_iff_le LowerAdjoint.closure_le_closed_iff_le

end PartialOrder

theorem closure_top [PartialOrder Œ±] [OrderTop Œ±] [Preorder Œ≤] {u : Œ≤ ‚Üí Œ±} (l : LowerAdjoint u) :
    u (l ‚ä§) = ‚ä§ :=
  l.closureOperator.closure_top
#align lower_adjoint.closure_top LowerAdjoint.closure_top

theorem closure_inf_le [SemilatticeInf Œ±] [Preorder Œ≤] {u : Œ≤ ‚Üí Œ±} (l : LowerAdjoint u) (x y : Œ±) :
    u (l (x ‚äì y)) ‚â§ u (l x) ‚äì u (l y) :=
  l.closureOperator.closure_inf_le x y
#align lower_adjoint.closure_inf_le LowerAdjoint.closure_inf_le

section SemilatticeSup

variable [SemilatticeSup Œ±] [Preorder Œ≤] {u : Œ≤ ‚Üí Œ±} (l : LowerAdjoint u)

theorem closure_sup_closure_le (x y : Œ±) : u (l x) ‚äî u (l y) ‚â§ u (l (x ‚äî y)) :=
  l.closureOperator.closure_sup_closure_le x y
#align lower_adjoint.closure_sup_closure_le LowerAdjoint.closure_sup_closure_le

theorem closure_sup_closure_left (x y : Œ±) : u (l (u (l x) ‚äî y)) = u (l (x ‚äî y)) :=
  l.closureOperator.closure_sup_closure_left x y
#align lower_adjoint.closure_sup_closure_left LowerAdjoint.closure_sup_closure_left

theorem closure_sup_closure_right (x y : Œ±) : u (l (x ‚äî u (l y))) = u (l (x ‚äî y)) :=
  l.closureOperator.closure_sup_closure_right x y
#align lower_adjoint.closure_sup_closure_right LowerAdjoint.closure_sup_closure_right

theorem closure_sup_closure (x y : Œ±) : u (l (u (l x) ‚äî u (l y))) = u (l (x ‚äî y)) :=
  l.closureOperator.closure_sup_closure x y
#align lower_adjoint.closure_sup_closure LowerAdjoint.closure_sup_closure

end SemilatticeSup

section CompleteLattice

variable [CompleteLattice Œ±] [Preorder Œ≤] {u : Œ≤ ‚Üí Œ±} (l : LowerAdjoint u)

theorem closure_iSup_closure (f : Œπ ‚Üí Œ±) : u (l (‚®Ü i, u (l (f i)))) = u (l (‚®Ü i, f i)) :=
  l.closureOperator.closure_iSup_closure _
#align lower_adjoint.closure_supr_closure LowerAdjoint.closure_iSup_closure

theorem closure_iSup‚ÇÇ_closure (f : ‚àÄ i, Œ∫ i ‚Üí Œ±) :
    u (l <| ‚®Ü (i) (j), u (l <| f i j)) = u (l <| ‚®Ü (i) (j), f i j) :=
  l.closureOperator.closure_iSup‚ÇÇ_closure _
#align lower_adjoint.closure_supr‚ÇÇ_closure LowerAdjoint.closure_iSup‚ÇÇ_closure

end CompleteLattice

-- Lemmas for `LowerAdjoint ((‚Üë) : Œ± ‚Üí Set Œ≤)`, where `SetLike Œ± Œ≤`
section CoeToSet

variable [SetLike Œ± Œ≤] (l : LowerAdjoint ((‚Üë) : Œ± ‚Üí Set Œ≤))

theorem subset_closure (s : Set Œ≤) : s ‚äÜ l s :=
  l.le_closure s
#align lower_adjoint.subset_closure LowerAdjoint.subset_closure

theorem not_mem_of_not_mem_closure {s : Set Œ≤} {P : Œ≤} (hP : P ‚àâ l s) : P ‚àâ s := fun h =>
  hP (subset_closure _ s h)
#align lower_adjoint.not_mem_of_not_mem_closure LowerAdjoint.not_mem_of_not_mem_closure

theorem le_iff_subset (s : Set Œ≤) (S : Œ±) : l s ‚â§ S ‚Üî s ‚äÜ S :=
  l.gc s S
#align lower_adjoint.le_iff_subset LowerAdjoint.le_iff_subset

theorem mem_iff (s : Set Œ≤) (x : Œ≤) : x ‚àà l s ‚Üî ‚àÄ S : Œ±, s ‚äÜ S ‚Üí x ‚àà S := by
  simp_rw [‚Üê SetLike.mem_coe, ‚Üê Set.singleton_subset_iff, ‚Üê l.le_iff_subset]
  -- ‚ä¢ toFun l {x} ‚â§ toFun l s ‚Üî ‚àÄ (S : Œ±), toFun l s ‚â§ S ‚Üí toFun l {x} ‚â§ S
  exact ‚ü®fun h S => h.trans, fun h => h _ le_rfl‚ü©
  -- üéâ no goals
#align lower_adjoint.mem_iff LowerAdjoint.mem_iff

theorem eq_of_le {s : Set Œ≤} {S : Œ±} (h‚ÇÅ : s ‚äÜ S) (h‚ÇÇ : S ‚â§ l s) : l s = S :=
  ((l.le_iff_subset _ _).2 h‚ÇÅ).antisymm h‚ÇÇ
#align lower_adjoint.eq_of_le LowerAdjoint.eq_of_le

theorem closure_union_closure_subset (x y : Œ±) : (l x : Set Œ≤) ‚à™ l y ‚äÜ l (x ‚à™ y) :=
  l.closure_sup_closure_le x y
#align lower_adjoint.closure_union_closure_subset LowerAdjoint.closure_union_closure_subset

@[simp]
theorem closure_union_closure_left (x y : Œ±) : l (l x ‚à™ y) = l (x ‚à™ y) :=
  SetLike.coe_injective (l.closure_sup_closure_left x y)
#align lower_adjoint.closure_union_closure_left LowerAdjoint.closure_union_closure_left

@[simp]
theorem closure_union_closure_right (x y : Œ±) : l (x ‚à™ l y) = l (x ‚à™ y) :=
  SetLike.coe_injective (l.closure_sup_closure_right x y)
#align lower_adjoint.closure_union_closure_right LowerAdjoint.closure_union_closure_right

theorem closure_union_closure (x y : Œ±) : l (l x ‚à™ l y) = l (x ‚à™ y) := by
  rw [closure_union_closure_right, closure_union_closure_left]
  -- üéâ no goals
#align lower_adjoint.closure_union_closure LowerAdjoint.closure_union_closure

@[simp]
theorem closure_iUnion_closure (f : Œπ ‚Üí Œ±) : l (‚ãÉ i, l (f i)) = l (‚ãÉ i, f i) :=
  SetLike.coe_injective <| l.closure_iSup_closure _
#align lower_adjoint.closure_Union_closure LowerAdjoint.closure_iUnion_closure

/- ./././Mathport/Syntax/Translate/Expr.lean:107:6: warning: expanding binder group (i j) -/
/- ./././Mathport/Syntax/Translate/Expr.lean:107:6: warning: expanding binder group (i j) -/
@[simp]
theorem closure_iUnion‚ÇÇ_closure (f : ‚àÄ i, Œ∫ i ‚Üí Œ±) :
    l (‚ãÉ (i) (j), l (f i j)) = l (‚ãÉ (i) (j), f i j) :=
  SetLike.coe_injective <| l.closure_iSup‚ÇÇ_closure _
#align lower_adjoint.closure_Union‚ÇÇ_closure LowerAdjoint.closure_iUnion‚ÇÇ_closure

end CoeToSet

end LowerAdjoint

/-! ### Translations between `GaloisConnection`, `LowerAdjoint`, `ClosureOperator` -/

/-- Every Galois connection induces a lower adjoint. -/
@[simps]
def GaloisConnection.lowerAdjoint [Preorder Œ±] [Preorder Œ≤] {l : Œ± ‚Üí Œ≤} {u : Œ≤ ‚Üí Œ±}
    (gc : GaloisConnection l u) : LowerAdjoint u
    where
  toFun := l
  gc' := gc
#align galois_connection.lower_adjoint GaloisConnection.lowerAdjoint
#align galois_connection.lower_adjoint_to_fun GaloisConnection.lowerAdjoint_toFun

/-- Every Galois connection induces a closure operator given by the composition. This is the partial
order version of the statement that every adjunction induces a monad. -/
@[simps!]
def GaloisConnection.closureOperator [PartialOrder Œ±] [Preorder Œ≤] {l : Œ± ‚Üí Œ≤} {u : Œ≤ ‚Üí Œ±}
    (gc : GaloisConnection l u) : ClosureOperator Œ± :=
  gc.lowerAdjoint.closureOperator
#align galois_connection.closure_operator GaloisConnection.closureOperator
#align galois_connection.closure_operator_apply GaloisConnection.closureOperator_apply

/-- The set of closed elements has a Galois insertion to the underlying type. -/
def _root_.ClosureOperator.gi [PartialOrder Œ±] (c : ClosureOperator Œ±) :
    GaloisInsertion c.toClosed (‚Üë) where
  choice x hx := ‚ü®x, hx.antisymm (c.le_closure x)‚ü©
  gc _ y := c.closure_le_closed_iff_le _ y.2
  le_l_u _ := c.le_closure _
  choice_eq x hx := le_antisymm (c.le_closure x) hx
#align closure_operator.gi ClosureOperator.gi

/-- The Galois insertion associated to a closure operator can be used to reconstruct the closure
operator.
Note that the inverse in the opposite direction does not hold in general. -/
@[simp]
theorem closureOperator_gi_self [PartialOrder Œ±] (c : ClosureOperator Œ±) :
    c.gi.gc.closureOperator = c := by
  ext x
  -- ‚ä¢ ‚Üë(GaloisConnection.closureOperator (_ : GaloisConnection (ClosureOperator.to ‚Ä¶
  rfl
  -- üéâ no goals
#align closure_operator_gi_self closureOperator_gi_self
