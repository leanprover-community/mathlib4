/-
Copyright (c) 2021 Aaron Anderson, YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Aaron Anderson, Kevin Buzzard, YaÃ«l Dillies, Eric Wieser
-/
import Mathlib.Data.Finset.Sigma
import Mathlib.Data.Finset.Pairwise
import Mathlib.Data.Finset.Powerset
import Mathlib.Data.Fintype.Basic

#align_import order.sup_indep from "leanprover-community/mathlib"@"c4c2ed622f43768eff32608d4a0f8a6cec1c047d"

/-!
# Supremum independence

In this file, we define supremum independence of indexed sets. An indexed family `f : Î¹ â†’ Î±` is
sup-independent if, for all `a`, `f a` and the supremum of the rest are disjoint.

## Main definitions

* `Finset.SupIndep s f`: a family of elements `f` are supremum independent on the finite set `s`.
* `CompleteLattice.SetIndependent s`: a set of elements are supremum independent.
* `CompleteLattice.Independent f`: a family of elements are supremum independent.

## Main statements

* In a distributive lattice, supremum independence is equivalent to pairwise disjointness:
  * `Finset.supIndep_iff_pairwiseDisjoint`
  * `CompleteLattice.setIndependent_iff_pairwiseDisjoint`
  * `CompleteLattice.independent_iff_pairwiseDisjoint`
* Otherwise, supremum independence is stronger than pairwise disjointness:
  * `Finset.SupIndep.pairwiseDisjoint`
  * `CompleteLattice.SetIndependent.pairwiseDisjoint`
  * `CompleteLattice.Independent.pairwiseDisjoint`

## Implementation notes

For the finite version, we avoid the "obvious" definition
`âˆ€ i âˆˆ s, Disjoint (f i) ((s.erase i).sup f)` because `erase` would require decidable equality on
`Î¹`.
-/


variable {Î± Î² Î¹ Î¹' : Type*}

/-! ### On lattices with a bottom element, via `Finset.sup` -/


namespace Finset

section Lattice

variable [Lattice Î±] [OrderBot Î±]

/-- Supremum independence of finite sets. We avoid the "obvious" definition using `s.erase i`
because `erase` would require decidable equality on `Î¹`. -/
def SupIndep (s : Finset Î¹) (f : Î¹ â†’ Î±) : Prop :=
  âˆ€ â¦ƒtâ¦„, t âŠ† s â†’ âˆ€ â¦ƒiâ¦„, i âˆˆ s â†’ i âˆ‰ t â†’ Disjoint (f i) (t.sup f)
#align finset.sup_indep Finset.SupIndep

variable {s t : Finset Î¹} {f : Î¹ â†’ Î±} {i : Î¹}

instance [DecidableEq Î¹] [DecidableEq Î±] : Decidable (SupIndep s f) := by
  refine @Finset.decidableForallOfDecidableSubsets _ _ _ (?_)
  -- âŠ¢ (t : Finset Î¹) â†’ t âŠ† s â†’ Decidable (âˆ€ â¦ƒi : Î¹â¦„, i âˆˆ s â†’ Â¬i âˆˆ t â†’ Disjoint (f  â€¦
  rintro t -
  -- âŠ¢ Decidable (âˆ€ â¦ƒi : Î¹â¦„, i âˆˆ s â†’ Â¬i âˆˆ t â†’ Disjoint (f i) (sup t f))
  refine @Finset.decidableDforallFinset _ _ _ (?_)
  -- âŠ¢ (a : Î¹) â†’ a âˆˆ s â†’ Decidable (Â¬a âˆˆ t â†’ Disjoint (f a) (sup t f))
  rintro i -
  -- âŠ¢ Decidable (Â¬i âˆˆ t â†’ Disjoint (f i) (sup t f))
  have : Decidable (Disjoint (f i) (sup t f)) := decidable_of_iff' (_ = âŠ¥) disjoint_iff
  -- âŠ¢ Decidable (Â¬i âˆˆ t â†’ Disjoint (f i) (sup t f))
  infer_instance
  -- ğŸ‰ no goals

theorem SupIndep.subset (ht : t.SupIndep f) (h : s âŠ† t) : s.SupIndep f := fun _ hu _ hi =>
  ht (hu.trans h) (h hi)
#align finset.sup_indep.subset Finset.SupIndep.subset

theorem supIndep_empty (f : Î¹ â†’ Î±) : (âˆ… : Finset Î¹).SupIndep f := fun _ _ a ha =>
  (not_mem_empty a ha).elim
#align finset.sup_indep_empty Finset.supIndep_empty

theorem supIndep_singleton (i : Î¹) (f : Î¹ â†’ Î±) : ({i} : Finset Î¹).SupIndep f :=
  fun s hs j hji hj => by
    rw [eq_empty_of_ssubset_singleton âŸ¨hs, fun h => hj (h hji)âŸ©, sup_empty]
    -- âŠ¢ Disjoint (f j) âŠ¥
    exact disjoint_bot_right
    -- ğŸ‰ no goals
#align finset.sup_indep_singleton Finset.supIndep_singleton

theorem SupIndep.pairwiseDisjoint (hs : s.SupIndep f) : (s : Set Î¹).PairwiseDisjoint f :=
  fun _ ha _ hb hab =>
    sup_singleton.subst <| hs (singleton_subset_iff.2 hb) ha <| not_mem_singleton.2 hab
#align finset.sup_indep.pairwise_disjoint Finset.SupIndep.pairwiseDisjoint

theorem SupIndep.le_sup_iff (hs : s.SupIndep f) (hts : t âŠ† s) (hi : i âˆˆ s) (hf : âˆ€ i, f i â‰  âŠ¥) :
    f i â‰¤ t.sup f â†” i âˆˆ t := by
  refine' âŸ¨fun h => _, le_supâŸ©
  -- âŠ¢ i âˆˆ t
  by_contra hit
  -- âŠ¢ False
  exact hf i (disjoint_self.1 <| (hs hts hi hit).mono_right h)
  -- ğŸ‰ no goals
#align finset.sup_indep.le_sup_iff Finset.SupIndep.le_sup_iff

/-- The RHS looks like the definition of `CompleteLattice.Independent`. -/
theorem supIndep_iff_disjoint_erase [DecidableEq Î¹] :
    s.SupIndep f â†” âˆ€ i âˆˆ s, Disjoint (f i) ((s.erase i).sup f) :=
  âŸ¨fun hs _ hi => hs (erase_subset _ _) hi (not_mem_erase _ _), fun hs _ ht i hi hit =>
    (hs i hi).mono_right (sup_mono fun _ hj => mem_erase.2 âŸ¨ne_of_mem_of_not_mem hj hit, ht hjâŸ©)âŸ©
#align finset.sup_indep_iff_disjoint_erase Finset.supIndep_iff_disjoint_erase

theorem SupIndep.image [DecidableEq Î¹] {s : Finset Î¹'} {g : Î¹' â†’ Î¹} (hs : s.SupIndep (f âˆ˜ g)) :
    (s.image g).SupIndep f := by
  intro t ht i hi hit
  -- âŠ¢ Disjoint (f i) (sup t f)
  rw [mem_image] at hi
  -- âŠ¢ Disjoint (f i) (sup t f)
  obtain âŸ¨i, hi, rflâŸ© := hi
  -- âŠ¢ Disjoint (f (g i)) (sup t f)
  haveI : DecidableEq Î¹' := Classical.decEq _
  -- âŠ¢ Disjoint (f (g i)) (sup t f)
  suffices hts : t âŠ† (s.erase i).image g
  -- âŠ¢ Disjoint (f (g i)) (sup t f)
  Â· refine' (supIndep_iff_disjoint_erase.1 hs i hi).mono_right ((sup_mono hts).trans _)
    -- âŠ¢ sup (Finset.image g (erase s i)) f â‰¤ sup (erase s i) (f âˆ˜ g)
    rw [sup_image]
    -- ğŸ‰ no goals
  rintro j hjt
  -- âŠ¢ j âˆˆ Finset.image g (erase s i)
  obtain âŸ¨j, hj, rflâŸ© := mem_image.1 (ht hjt)
  -- âŠ¢ g j âˆˆ Finset.image g (erase s i)
  exact mem_image_of_mem _ (mem_erase.2 âŸ¨ne_of_apply_ne g (ne_of_mem_of_not_mem hjt hit), hjâŸ©)
  -- ğŸ‰ no goals
#align finset.sup_indep.image Finset.SupIndep.image

theorem supIndep_map {s : Finset Î¹'} {g : Î¹' â†ª Î¹} : (s.map g).SupIndep f â†” s.SupIndep (f âˆ˜ g) := by
  refine' âŸ¨fun hs t ht i hi hit => _, fun hs => _âŸ©
  -- âŠ¢ Disjoint ((f âˆ˜ â†‘g) i) (sup t (f âˆ˜ â†‘g))
  Â· rw [â† sup_map]
    -- âŠ¢ Disjoint ((f âˆ˜ â†‘g) i) (sup (map g t) f)
    exact hs (map_subset_map.2 ht) ((mem_map' _).2 hi) (by rwa [mem_map'])
    -- ğŸ‰ no goals
  Â· classical
    rw [map_eq_image]
    exact hs.image
#align finset.sup_indep_map Finset.supIndep_map

@[simp]
theorem supIndep_pair [DecidableEq Î¹] {i j : Î¹} (hij : i â‰  j) :
    ({i, j} : Finset Î¹).SupIndep f â†” Disjoint (f i) (f j) :=
  âŸ¨fun h => h.pairwiseDisjoint (by simp) (by simp) hij,
                                   -- ğŸ‰ no goals
                                             -- ğŸ‰ no goals
   fun h => by
    rw [supIndep_iff_disjoint_erase]
    -- âŠ¢ âˆ€ (i_1 : Î¹), i_1 âˆˆ {i, j} â†’ Disjoint (f i_1) (sup (erase {i, j} i_1) f)
    intro k hk
    -- âŠ¢ Disjoint (f k) (sup (erase {i, j} k) f)
    rw [Finset.mem_insert, Finset.mem_singleton] at hk
    -- âŠ¢ Disjoint (f k) (sup (erase {i, j} k) f)
    obtain rfl | rfl := hk
    -- âŠ¢ Disjoint (f k) (sup (erase {k, j} k) f)
    Â· convert h using 1
      -- âŠ¢ sup (erase {k, j} k) f = f j
      rw [Finset.erase_insert, Finset.sup_singleton]
      -- âŠ¢ Â¬k âˆˆ {j}
      simpa using hij
      -- ğŸ‰ no goals
    Â· convert h.symm using 1
      -- âŠ¢ sup (erase {i, k} k) f = f i
      have : ({i, k} : Finset Î¹).erase k = {i} := by
        ext
        rw [mem_erase, mem_insert, mem_singleton, mem_singleton, and_or_left, Ne.def,
          not_and_self_iff, or_false_iff, and_iff_right_of_imp]
        rintro rfl
        exact hij
      rw [this, Finset.sup_singleton]âŸ©
      -- ğŸ‰ no goals
#align finset.sup_indep_pair Finset.supIndep_pair

theorem supIndep_univ_bool (f : Bool â†’ Î±) :
    (Finset.univ : Finset Bool).SupIndep f â†” Disjoint (f false) (f true) :=
  haveI : true â‰  false := by simp only [Ne.def, not_false_iff]
                             -- ğŸ‰ no goals
  (supIndep_pair this).trans disjoint_comm
#align finset.sup_indep_univ_bool Finset.supIndep_univ_bool

@[simp]
theorem supIndep_univ_fin_two (f : Fin 2 â†’ Î±) :
    (Finset.univ : Finset (Fin 2)).SupIndep f â†” Disjoint (f 0) (f 1) :=
  haveI : (0 : Fin 2) â‰  1 := by simp
                                -- ğŸ‰ no goals
  supIndep_pair this
#align finset.sup_indep_univ_fin_two Finset.supIndep_univ_fin_two

theorem SupIndep.attach (hs : s.SupIndep f) : s.attach.SupIndep fun a => f a := by
  intro t _ i _ hi
  -- âŠ¢ Disjoint ((fun a => f â†‘a) i) (sup t fun a => f â†‘a)
  classical
    have : (fun (a : { x // x âˆˆ s }) => f â†‘a) = f âˆ˜ (fun a : { x // x âˆˆ s } => â†‘a) := rfl
    rw [this, â† Finset.sup_image]
    refine' hs (image_subset_iff.2 fun (j : { x // x âˆˆ s }) _ => j.2) i.2 fun hi' => hi _
    rw [mem_image] at hi'
    obtain âŸ¨j, hj, hjiâŸ© := hi'
    rwa [Subtype.ext hji] at hj
#align finset.sup_indep.attach Finset.SupIndep.attach

/-
Porting note: simpNF linter returns

"Left-hand side does not simplify, when using the simp lemma on itself."

However, simp does indeed solve the following. leanprover/std4#71 is related.

example {Î± Î¹} [Lattice Î±] [OrderBot Î±] (s : Finset Î¹) (f : Î¹ â†’ Î±) :
  (s.attach.SupIndep fun a => f a) â†” s.SupIndep f := by simp
-/
@[simp, nolint simpNF]
theorem supIndep_attach : (s.attach.SupIndep fun a => f a) â†” s.SupIndep f := by
  refine' âŸ¨fun h t ht i his hit => _, SupIndep.attachâŸ©
  -- âŠ¢ Disjoint (f i) (sup t f)
  classical
  convert h (filter_subset (fun (i : { x // x âˆˆ s }) => (i : Î¹) âˆˆ t) _) (mem_attach _ âŸ¨i, â€¹_â€ºâŸ©)
    fun hi => hit <| by simpa using hi using 1
  refine' eq_of_forall_ge_iff _
  simp only [Finset.sup_le_iff, mem_filter, mem_attach, true_and_iff, Function.comp_apply,
    Subtype.forall, Subtype.coe_mk]
  exact fun a => forall_congr' fun j => âŸ¨fun h _ => h, fun h hj => h (ht hj) hjâŸ©
#align finset.sup_indep_attach Finset.supIndep_attach

end Lattice

section DistribLattice

variable [DistribLattice Î±] [OrderBot Î±] {s : Finset Î¹} {f : Î¹ â†’ Î±}

theorem supIndep_iff_pairwiseDisjoint : s.SupIndep f â†” (s : Set Î¹).PairwiseDisjoint f :=
  âŸ¨SupIndep.pairwiseDisjoint, fun hs _ ht _ hi hit =>
    Finset.disjoint_sup_right.2 fun _ hj => hs hi (ht hj) (ne_of_mem_of_not_mem hj hit).symmâŸ©
#align finset.sup_indep_iff_pairwise_disjoint Finset.supIndep_iff_pairwiseDisjoint

alias âŸ¨sup_indep.pairwise_disjoint, _root_.Set.PairwiseDisjoint.supIndepâŸ© :=
  supIndep_iff_pairwiseDisjoint
#align set.pairwise_disjoint.sup_indep Set.PairwiseDisjoint.supIndep

/-- Bind operation for `SupIndep`. -/
theorem SupIndep.sup [DecidableEq Î¹] {s : Finset Î¹'} {g : Î¹' â†’ Finset Î¹} {f : Î¹ â†’ Î±}
    (hs : s.SupIndep fun i => (g i).sup f) (hg : âˆ€ i' âˆˆ s, (g i').SupIndep f) :
    (s.sup g).SupIndep f := by
  simp_rw [supIndep_iff_pairwiseDisjoint] at hs hg âŠ¢
  -- âŠ¢ Set.PairwiseDisjoint (â†‘(Finset.sup s g)) f
  rw [sup_eq_biUnion, coe_biUnion]
  -- âŠ¢ Set.PairwiseDisjoint (â‹ƒ (x : Î¹') (_ : x âˆˆ â†‘s), â†‘(g x)) f
  exact hs.biUnion_finset hg
  -- ğŸ‰ no goals
#align finset.sup_indep.sup Finset.SupIndep.sup

/-- Bind operation for `SupIndep`. -/
theorem SupIndep.biUnion [DecidableEq Î¹] {s : Finset Î¹'} {g : Î¹' â†’ Finset Î¹} {f : Î¹ â†’ Î±}
    (hs : s.SupIndep fun i => (g i).sup f) (hg : âˆ€ i' âˆˆ s, (g i').SupIndep f) :
    (s.biUnion g).SupIndep f := by
  rw [â† sup_eq_biUnion]
  -- âŠ¢ SupIndep (Finset.sup s g) f
  exact hs.sup hg
  -- ğŸ‰ no goals
#align finset.sup_indep.bUnion Finset.SupIndep.biUnion

/-- Bind operation for `SupIndep`. -/
theorem SupIndep.sigma {Î² : Î¹ â†’ Type*} {s : Finset Î¹} {g : âˆ€ i, Finset (Î² i)} {f : Sigma Î² â†’ Î±}
    (hs : s.SupIndep fun i => (g i).sup fun b => f âŸ¨i, bâŸ©)
    (hg : âˆ€ i âˆˆ s, (g i).SupIndep fun b => f âŸ¨i, bâŸ©) : (s.sigma g).SupIndep f := by
  rintro t ht âŸ¨i, bâŸ© hi hit
  -- âŠ¢ Disjoint (f { fst := i, snd := b }) (Finset.sup t f)
  rw [Finset.disjoint_sup_right]
  -- âŠ¢ âˆ€ â¦ƒi_1 : (i : Î¹) Ã— Î² iâ¦„, i_1 âˆˆ t â†’ Disjoint (f { fst := i, snd := b }) (f i_1)
  rintro âŸ¨j, câŸ© hj
  -- âŠ¢ Disjoint (f { fst := i, snd := b }) (f { fst := j, snd := c })
  have hbc := (ne_of_mem_of_not_mem hj hit).symm
  -- âŠ¢ Disjoint (f { fst := i, snd := b }) (f { fst := j, snd := c })
  replace hj := ht hj
  -- âŠ¢ Disjoint (f { fst := i, snd := b }) (f { fst := j, snd := c })
  rw [mem_sigma] at hi hj
  -- âŠ¢ Disjoint (f { fst := i, snd := b }) (f { fst := j, snd := c })
  obtain rfl | hij := eq_or_ne i j
  -- âŠ¢ Disjoint (f { fst := i, snd := b }) (f { fst := i, snd := c })
  Â· exact (hg _ hj.1).pairwiseDisjoint hi.2 hj.2 (sigma_mk_injective.ne_iff.1 hbc)
    -- ğŸ‰ no goals
  Â· refine' (hs.pairwiseDisjoint hi.1 hj.1 hij).mono _ _
    -- âŠ¢ f { fst := i, snd := b } â‰¤ (fun i => Finset.sup (g i) fun b => f { fst := i, â€¦
    Â· convert le_sup (Î± := Î±) hi.2; simp
      -- âŠ¢ f { fst := i, snd := b } = f { fst := { fst := i, snd := b }.fst, snd := { f â€¦
                                    -- ğŸ‰ no goals
    Â· convert le_sup (Î± := Î±) hj.2; simp
      -- âŠ¢ f { fst := j, snd := c } = f { fst := { fst := j, snd := c }.fst, snd := { f â€¦
                                    -- ğŸ‰ no goals
#align finset.sup_indep.sigma Finset.SupIndep.sigma

theorem SupIndep.product {s : Finset Î¹} {t : Finset Î¹'} {f : Î¹ Ã— Î¹' â†’ Î±}
    (hs : s.SupIndep fun i => t.sup fun i' => f (i, i'))
    (ht : t.SupIndep fun i' => s.sup fun i => f (i, i')) : (s Ã—Ë¢ t).SupIndep f := by
  rintro u hu âŸ¨i, i'âŸ© hi hiu
  -- âŠ¢ Disjoint (f (i, i')) (Finset.sup u f)
  rw [Finset.disjoint_sup_right]
  -- âŠ¢ âˆ€ â¦ƒi_1 : Î¹ Ã— Î¹'â¦„, i_1 âˆˆ u â†’ Disjoint (f (i, i')) (f i_1)
  rintro âŸ¨j, j'âŸ© hj
  -- âŠ¢ Disjoint (f (i, i')) (f (j, j'))
  have hij := (ne_of_mem_of_not_mem hj hiu).symm
  -- âŠ¢ Disjoint (f (i, i')) (f (j, j'))
  replace hj := hu hj
  -- âŠ¢ Disjoint (f (i, i')) (f (j, j'))
  rw [mem_product] at hi hj
  -- âŠ¢ Disjoint (f (i, i')) (f (j, j'))
  obtain rfl | hij := eq_or_ne i j
  -- âŠ¢ Disjoint (f (i, i')) (f (i, j'))
  Â· refine' (ht.pairwiseDisjoint hi.2 hj.2 <| (Prod.mk.inj_left _).ne_iff.1 hij).mono _ _
    -- âŠ¢ f (i, i') â‰¤ (fun i' => Finset.sup s fun i => f (i, i')) (i, i').snd
    Â· convert le_sup (Î± := Î±) hi.1; simp
      -- âŠ¢ f (i, i') = f ((i, i').fst, (i, i').snd)
                                    -- ğŸ‰ no goals
    Â· convert le_sup (Î± := Î±) hj.1; simp
      -- âŠ¢ f (i, j') = f ((i, j').fst, (i, j').snd)
                                    -- ğŸ‰ no goals
  Â· refine' (hs.pairwiseDisjoint hi.1 hj.1 hij).mono _ _
    -- âŠ¢ f (i, i') â‰¤ (fun i => Finset.sup t fun i' => f (i, i')) (i, i').fst
    Â· convert le_sup (Î± := Î±) hi.2; simp
      -- âŠ¢ f (i, i') = f ((i, i').fst, (i, i').snd)
                                    -- ğŸ‰ no goals
    Â· convert le_sup (Î± := Î±) hj.2; simp
      -- âŠ¢ f (j, j') = f ((j, j').fst, (j, j').snd)
                                    -- ğŸ‰ no goals
#align finset.sup_indep.product Finset.SupIndep.product

theorem supIndep_product_iff {s : Finset Î¹} {t : Finset Î¹'} {f : Î¹ Ã— Î¹' â†’ Î±} :
    (s.product t).SupIndep f â†” (s.SupIndep fun i => t.sup fun i' => f (i, i'))
      âˆ§ t.SupIndep fun i' => s.sup fun i => f (i, i') := by
  refine' âŸ¨_, fun h => h.1.product h.2âŸ©
  -- âŠ¢ SupIndep (Finset.product s t) f â†’ (SupIndep s fun i => sup t fun i' => f (i, â€¦
  simp_rw [supIndep_iff_pairwiseDisjoint]
  -- âŠ¢ Set.PairwiseDisjoint (â†‘(Finset.product s t)) f â†’ (Set.PairwiseDisjoint â†‘s fu â€¦
  refine' fun h => âŸ¨fun i hi j hj hij => _, fun i hi j hj hij => _âŸ© <;>
  -- âŠ¢ (Disjoint on fun i => sup t fun i' => f (i, i')) i j
      simp_rw [Function.onFun, Finset.disjoint_sup_left, Finset.disjoint_sup_right] <;>
      -- âŠ¢ âˆ€ â¦ƒi_1 : Î¹'â¦„, i_1 âˆˆ t â†’ âˆ€ â¦ƒi_2 : Î¹'â¦„, i_2 âˆˆ t â†’ Disjoint (f (i, i_1)) (f (j, â€¦
      -- âŠ¢ âˆ€ â¦ƒi_1 : Î¹â¦„, i_1 âˆˆ s â†’ âˆ€ â¦ƒi_2 : Î¹â¦„, i_2 âˆˆ s â†’ Disjoint (f (i_1, i)) (f (i_2, â€¦
    intro i' hi' j' hj'
    -- âŠ¢ Disjoint (f (i, i')) (f (j, j'))
    -- âŠ¢ Disjoint (f (i', i)) (f (j', j))
  Â· exact h (mk_mem_product hi hi') (mk_mem_product hj hj') (ne_of_apply_ne Prod.fst hij)
    -- ğŸ‰ no goals
  Â· exact h (mk_mem_product hi' hi) (mk_mem_product hj' hj) (ne_of_apply_ne Prod.snd hij)
    -- ğŸ‰ no goals
#align finset.sup_indep_product_iff Finset.supIndep_product_iff

end DistribLattice

end Finset

/-! ### On complete lattices via `sSup` -/


namespace CompleteLattice

variable [CompleteLattice Î±]

open Set Function

/-- An independent set of elements in a complete lattice is one in which every element is disjoint
  from the `Sup` of the rest. -/
def SetIndependent (s : Set Î±) : Prop :=
  âˆ€ â¦ƒaâ¦„, a âˆˆ s â†’ Disjoint a (sSup (s \ {a}))
#align complete_lattice.set_independent CompleteLattice.SetIndependent

variable {s : Set Î±} (hs : SetIndependent s)

@[simp]
theorem setIndependent_empty : SetIndependent (âˆ… : Set Î±) := fun x hx =>
  (Set.not_mem_empty x hx).elim
#align complete_lattice.set_independent_empty CompleteLattice.setIndependent_empty

theorem SetIndependent.mono {t : Set Î±} (hst : t âŠ† s) : SetIndependent t := fun _ ha =>
  (hs (hst ha)).mono_right (sSup_le_sSup (diff_subset_diff_left hst))
#align complete_lattice.set_independent.mono CompleteLattice.SetIndependent.mono

/-- If the elements of a set are independent, then any pair within that set is disjoint. -/
theorem SetIndependent.pairwiseDisjoint : s.PairwiseDisjoint id := fun _ hx y hy h =>
  disjoint_sSup_right (hs hx) ((mem_diff y).mpr âŸ¨hy, h.symmâŸ©)
#align complete_lattice.set_independent.pairwise_disjoint CompleteLattice.SetIndependent.pairwiseDisjoint

theorem setIndependent_pair {a b : Î±} (hab : a â‰  b) :
    SetIndependent ({a, b} : Set Î±) â†” Disjoint a b := by
  constructor
  -- âŠ¢ SetIndependent {a, b} â†’ Disjoint a b
  Â· intro h
    -- âŠ¢ Disjoint a b
    exact h.pairwiseDisjoint (mem_insert _ _) (mem_insert_of_mem _ (mem_singleton _)) hab
    -- ğŸ‰ no goals
  Â· rintro h c ((rfl : c = a) | (rfl : c = b))
    -- âŠ¢ Disjoint c (sSup ({c, b} \ {c}))
    Â· convert h using 1
      -- âŠ¢ sSup ({c, b} \ {c}) = b
      simp [hab, sSup_singleton]
      -- ğŸ‰ no goals
    Â· convert h.symm using 1
      -- âŠ¢ sSup ({a, c} \ {c}) = a
      simp [hab, sSup_singleton]
      -- ğŸ‰ no goals
#align complete_lattice.set_independent_pair CompleteLattice.setIndependent_pair

/-- If the elements of a set are independent, then any element is disjoint from the `sSup` of some
subset of the rest. -/
theorem SetIndependent.disjoint_sSup {x : Î±} {y : Set Î±} (hx : x âˆˆ s) (hy : y âŠ† s) (hxy : x âˆ‰ y) :
    Disjoint x (sSup y) := by
  have := (hs.mono <| insert_subset_iff.mpr âŸ¨hx, hyâŸ©) (mem_insert x _)
  -- âŠ¢ Disjoint x (sSup y)
  rw [insert_diff_of_mem _ (mem_singleton _), diff_singleton_eq_self hxy] at this
  -- âŠ¢ Disjoint x (sSup y)
  exact this
  -- ğŸ‰ no goals
#align complete_lattice.set_independent.disjoint_Sup CompleteLattice.SetIndependent.disjoint_sSup

/-- An independent indexed family of elements in a complete lattice is one in which every element
  is disjoint from the `iSup` of the rest.

  Example: an indexed family of non-zero elements in a
  vector space is linearly independent iff the indexed family of subspaces they generate is
  independent in this sense.

  Example: an indexed family of submodules of a module is independent in this sense if
  and only the natural map from the direct sum of the submodules to the module is injective. -/
-- Porting note: needed to use `_H`
def Independent {Î¹ : Sort*} {Î± : Type*} [CompleteLattice Î±] (t : Î¹ â†’ Î±) : Prop :=
  âˆ€ i : Î¹, Disjoint (t i) (â¨† (j) (_ : j â‰  i), t j)
#align complete_lattice.independent CompleteLattice.Independent

theorem setIndependent_iff {Î± : Type*} [CompleteLattice Î±] (s : Set Î±) :
    SetIndependent s â†” Independent ((â†‘) : s â†’ Î±) := by
  simp_rw [Independent, SetIndependent, SetCoe.forall, sSup_eq_iSup]
  -- âŠ¢ (âˆ€ â¦ƒa : Î±â¦„, a âˆˆ s â†’ Disjoint a (â¨† (a_2 : Î±) (_ : a_2 âˆˆ s \ {a}), a_2)) â†” âˆ€ ( â€¦
  refine' forallâ‚‚_congr fun a ha => _
  -- âŠ¢ Disjoint a (â¨† (a_1 : Î±) (_ : a_1 âˆˆ s \ {a}), a_1) â†” Disjoint a (â¨† (j : { x / â€¦
  simp [iSup_subtype, iSup_and]
  -- ğŸ‰ no goals
#align complete_lattice.set_independent_iff CompleteLattice.setIndependent_iff

variable {t : Î¹ â†’ Î±} (ht : Independent t)

theorem independent_def : Independent t â†” âˆ€ i : Î¹, Disjoint (t i) (â¨† (j) (_ : j â‰  i), t j) :=
  Iff.rfl
#align complete_lattice.independent_def CompleteLattice.independent_def

theorem independent_def' : Independent t â†” âˆ€ i, Disjoint (t i) (sSup (t '' { j | j â‰  i })) := by
  simp_rw [sSup_image]
  -- âŠ¢ Independent t â†” âˆ€ (i : Î¹), Disjoint (t i) (â¨† (a : Î¹) (_ : a âˆˆ {j | j â‰  i}),  â€¦
  rfl
  -- ğŸ‰ no goals
#align complete_lattice.independent_def' CompleteLattice.independent_def'

theorem independent_def'' :
    Independent t â†” âˆ€ i, Disjoint (t i) (sSup { a | âˆƒ (j : _) (_ : j â‰  i), t j = a }) := by
  rw [independent_def']
  -- âŠ¢ (âˆ€ (i : Î¹), Disjoint (t i) (sSup (t '' {j | j â‰  i}))) â†” âˆ€ (i : Î¹), Disjoint  â€¦
  aesop
  -- ğŸ‰ no goals
#align complete_lattice.independent_def'' CompleteLattice.independent_def''

@[simp]
theorem independent_empty (t : Empty â†’ Î±) : Independent t :=
  fun.
#align complete_lattice.independent_empty CompleteLattice.independent_empty

@[simp]
theorem independent_pempty (t : PEmpty â†’ Î±) : Independent t :=
  fun.
#align complete_lattice.independent_pempty CompleteLattice.independent_pempty

/-- If the elements of a set are independent, then any pair within that set is disjoint. -/
theorem Independent.pairwiseDisjoint : Pairwise (Disjoint on t) := fun x y h =>
  disjoint_sSup_right (ht x) âŸ¨y, iSup_pos h.symmâŸ©
#align complete_lattice.independent.pairwise_disjoint CompleteLattice.Independent.pairwiseDisjoint

theorem Independent.mono {s t : Î¹ â†’ Î±} (hs : Independent s) (hst : t â‰¤ s) : Independent t :=
  fun i => (hs i).mono (hst i) <| iSupâ‚‚_mono fun j _ => hst j
#align complete_lattice.independent.mono CompleteLattice.Independent.mono

/-- Composing an independent indexed family with an injective function on the index results in
another indepedendent indexed family. -/
theorem Independent.comp {Î¹ Î¹' : Sort*} {t : Î¹ â†’ Î±} {f : Î¹' â†’ Î¹} (ht : Independent t)
    (hf : Injective f) : Independent (t âˆ˜ f) := fun i =>
  (ht (f i)).mono_right <| by
    refine' (iSup_mono fun i => _).trans (iSup_comp_le _ f)
    -- âŠ¢ â¨† (_ : i â‰  iâœ), (t âˆ˜ f) i â‰¤ â¨† (_ : f i â‰  f iâœ), t (f i)
    exact iSup_const_mono hf.ne
    -- ğŸ‰ no goals
#align complete_lattice.independent.comp CompleteLattice.Independent.comp

theorem Independent.comp' {Î¹ Î¹' : Sort*} {t : Î¹ â†’ Î±} {f : Î¹' â†’ Î¹} (ht : Independent <| t âˆ˜ f)
    (hf : Surjective f) : Independent t := by
  intro i
  -- âŠ¢ Disjoint (t i) (â¨† (j : Î¹) (_ : j â‰  i), t j)
  obtain âŸ¨i', rflâŸ© := hf i
  -- âŠ¢ Disjoint (t (f i')) (â¨† (j : Î¹) (_ : j â‰  f i'), t j)
  rw [â† hf.iSup_comp]
  -- âŠ¢ Disjoint (t (f i')) (â¨† (x : Î¹') (_ : f x â‰  f i'), t (f x))
  exact (ht i').mono_right (biSup_mono fun j' hij => mt (congr_arg f) hij)
  -- ğŸ‰ no goals
#align complete_lattice.independent.comp' CompleteLattice.Independent.comp'

theorem Independent.setIndependent_range (ht : Independent t) : SetIndependent <| range t := by
  rw [setIndependent_iff]
  -- âŠ¢ Independent Subtype.val
  rw [â† coe_comp_rangeFactorization t] at ht
  -- âŠ¢ Independent Subtype.val
  exact ht.comp' surjective_onto_range
  -- ğŸ‰ no goals
#align complete_lattice.independent.set_independent_range CompleteLattice.Independent.setIndependent_range

theorem Independent.injective (ht : Independent t) (h_ne_bot : âˆ€ i, t i â‰  âŠ¥) : Injective t := by
  intro i j h
  -- âŠ¢ i = j
  by_contra' contra
  -- âŠ¢ False
  apply h_ne_bot j
  -- âŠ¢ t j = âŠ¥
  suffices t j â‰¤ â¨† (k) (_ : k â‰  i), t k by
    replace ht := (ht i).mono_right this
    rwa [h, disjoint_self] at ht
  replace contra : j â‰  i
  -- âŠ¢ j â‰  i
  Â· exact Ne.symm contra
    -- ğŸ‰ no goals
  -- Porting note: needs explicit `f`
  exact @le_iSupâ‚‚ _ _ _ _ (fun x _ => t x) j contra
  -- ğŸ‰ no goals
#align complete_lattice.independent.injective CompleteLattice.Independent.injective

theorem independent_pair {i j : Î¹} (hij : i â‰  j) (huniv : âˆ€ k, k = i âˆ¨ k = j) :
    Independent t â†” Disjoint (t i) (t j) := by
  constructor
  -- âŠ¢ Independent t â†’ Disjoint (t i) (t j)
  Â· exact fun h => h.pairwiseDisjoint hij
    -- ğŸ‰ no goals
  Â· rintro h k
    -- âŠ¢ Disjoint (t k) (â¨† (j : Î¹) (_ : j â‰  k), t j)
    obtain rfl | rfl := huniv k
    -- âŠ¢ Disjoint (t k) (â¨† (j : Î¹) (_ : j â‰  k), t j)
    Â· refine' h.mono_right (iSup_le fun i => iSup_le fun hi => Eq.le _)
      -- âŠ¢ t i = t j
      rw [(huniv i).resolve_left hi]
      -- ğŸ‰ no goals
    Â· refine' h.symm.mono_right (iSup_le fun j => iSup_le fun hj => Eq.le _)
      -- âŠ¢ t j = t i
      rw [(huniv j).resolve_right hj]
      -- ğŸ‰ no goals
#align complete_lattice.independent_pair CompleteLattice.independent_pair

/-- Composing an independent indexed family with an order isomorphism on the elements results in
another independent indexed family. -/
theorem Independent.map_orderIso {Î¹ : Sort*} {Î± Î² : Type*} [CompleteLattice Î±]
    [CompleteLattice Î²] (f : Î± â‰ƒo Î²) {a : Î¹ â†’ Î±} (ha : Independent a) : Independent (f âˆ˜ a) :=
  fun i => ((ha i).map_orderIso f).mono_right (f.monotone.le_map_iSupâ‚‚ _)
#align complete_lattice.independent.map_order_iso CompleteLattice.Independent.map_orderIso

@[simp]
theorem independent_map_orderIso_iff {Î¹ : Sort*} {Î± Î² : Type*} [CompleteLattice Î±]
    [CompleteLattice Î²] (f : Î± â‰ƒo Î²) {a : Î¹ â†’ Î±} : Independent (f âˆ˜ a) â†” Independent a :=
  âŸ¨fun h =>
    have hf : f.symm âˆ˜ f âˆ˜ a = a := congr_arg (Â· âˆ˜ a) f.left_inv.comp_eq_id
    hf â–¸ h.map_orderIso f.symm,
    fun h => h.map_orderIso fâŸ©
#align complete_lattice.independent_map_order_iso_iff CompleteLattice.independent_map_orderIso_iff

/-- If the elements of a set are independent, then any element is disjoint from the `iSup` of some
subset of the rest. -/
theorem Independent.disjoint_biSup {Î¹ : Type*} {Î± : Type*} [CompleteLattice Î±] {t : Î¹ â†’ Î±}
    (ht : Independent t) {x : Î¹} {y : Set Î¹} (hx : x âˆ‰ y) : Disjoint (t x) (â¨† i âˆˆ y, t i) :=
  Disjoint.mono_right (biSup_mono fun _ hi => (ne_of_mem_of_not_mem hi hx : _)) (ht x)
#align complete_lattice.independent.disjoint_bsupr CompleteLattice.Independent.disjoint_biSup

end CompleteLattice

theorem CompleteLattice.independent_iff_supIndep [CompleteLattice Î±] {s : Finset Î¹} {f : Î¹ â†’ Î±} :
    CompleteLattice.Independent (f âˆ˜ ((â†‘) : s â†’ Î¹)) â†” s.SupIndep f := by
  classical
    rw [Finset.supIndep_iff_disjoint_erase]
    refine' Subtype.forall.trans (forallâ‚‚_congr fun a b => _)
    rw [Finset.sup_eq_iSup]
    congr! 1
    refine' iSup_subtype.trans _
    congr! 1
    simp [iSup_and, @iSup_comm _ (_ âˆˆ s)]
#align complete_lattice.independent_iff_sup_indep CompleteLattice.independent_iff_supIndep

alias âŸ¨CompleteLattice.Independent.supIndep, Finset.SupIndep.independentâŸ© :=
  CompleteLattice.independent_iff_supIndep
#align complete_lattice.independent.sup_indep CompleteLattice.Independent.supIndep
#align finset.sup_indep.independent Finset.SupIndep.independent

/-- A variant of `CompleteLattice.independent_iff_supIndep` for `Fintype`s. -/
theorem CompleteLattice.independent_iff_supIndep_univ [CompleteLattice Î±] [Fintype Î¹] {f : Î¹ â†’ Î±} :
    CompleteLattice.Independent f â†” Finset.univ.SupIndep f := by
  classical
    simp [Finset.supIndep_iff_disjoint_erase, CompleteLattice.Independent, Finset.sup_eq_iSup]
#align complete_lattice.independent_iff_sup_indep_univ CompleteLattice.independent_iff_supIndep_univ

alias âŸ¨CompleteLattice.Independent.sup_indep_univ, Finset.SupIndep.independent_of_univâŸ© :=
  CompleteLattice.independent_iff_supIndep_univ
#align complete_lattice.independent.sup_indep_univ CompleteLattice.Independent.sup_indep_univ
#align finset.sup_indep.independent_of_univ Finset.SupIndep.independent_of_univ

section Frame

namespace CompleteLattice

variable [Order.Frame Î±]

theorem setIndependent_iff_pairwiseDisjoint {s : Set Î±} :
    SetIndependent s â†” s.PairwiseDisjoint id :=
  âŸ¨SetIndependent.pairwiseDisjoint, fun hs _ hi =>
    disjoint_sSup_iff.2 fun _ hj => hs hi hj.1 <| Ne.symm hj.2âŸ©
#align complete_lattice.set_independent_iff_pairwise_disjoint CompleteLattice.setIndependent_iff_pairwiseDisjoint

alias âŸ¨_, _root_.Set.PairwiseDisjoint.setIndependentâŸ© := setIndependent_iff_pairwiseDisjoint
#align set.pairwise_disjoint.set_independent Set.PairwiseDisjoint.setIndependent

theorem independent_iff_pairwiseDisjoint {f : Î¹ â†’ Î±} : Independent f â†” Pairwise (Disjoint on f) :=
  âŸ¨Independent.pairwiseDisjoint, fun hs _ =>
    disjoint_iSup_iff.2 fun _ => disjoint_iSup_iff.2 fun hij => hs hij.symmâŸ©
#align complete_lattice.independent_iff_pairwise_disjoint CompleteLattice.independent_iff_pairwiseDisjoint

end CompleteLattice

end Frame
