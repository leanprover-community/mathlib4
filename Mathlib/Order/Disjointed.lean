/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, YaÃ«l Dillies
-/
import Mathlib.Order.PartialSups

#align_import order.disjointed from "leanprover-community/mathlib"@"f7fc89d5d5ff1db2d1242c7bb0e9062ce47ef47c"

/-!
# Consecutive differences of sets

This file defines the way to make a sequence of elements into a sequence of disjoint elements with
the same partial sups.

For a sequence `f : â„• â†’ Î±`, this new sequence will be `f 0`, `f 1 \ f 0`, `f 2 \ (f 0 âŠ” f 1)`.
It is actually unique, as `disjointed_unique` shows.

## Main declarations

* `disjointed f`: The sequence `f 0`, `f 1 \ f 0`, `f 2 \ (f 0 âŠ” f 1)`, ....
* `partialSups_disjointed`: `disjointed f` has the same partial sups as `f`.
* `disjoint_disjointed`: The elements of `disjointed f` are pairwise disjoint.
* `disjointed_unique`: `disjointed f` is the only pairwise disjoint sequence having the same partial
  sups as `f`.
* `iSup_disjointed`: `disjointed f` has the same supremum as `f`. Limiting case of
  `partialSups_disjointed`.

We also provide set notation variants of some lemmas.

## TODO

Find a useful statement of `disjointedRec_succ`.

One could generalize `disjointed` to any locally finite bot preorder domain, in place of `â„•`.
Related to the TODO in the module docstring of `Mathlib.Order.PartialSups`.
-/


variable {Î± Î² : Type*}

section GeneralizedBooleanAlgebra

variable [GeneralizedBooleanAlgebra Î±]

/-- If `f : â„• â†’ Î±` is a sequence of elements, then `disjointed f` is the sequence formed by
subtracting each element from the nexts. This is the unique disjoint sequence whose partial sups
are the same as the original sequence. -/
def disjointed (f : â„• â†’ Î±) : â„• â†’ Î±
  | 0 => f 0
  | n + 1 => f (n + 1) \ partialSups f n
#align disjointed disjointed

@[simp]
theorem disjointed_zero (f : â„• â†’ Î±) : disjointed f 0 = f 0 :=
  rfl
#align disjointed_zero disjointed_zero

theorem disjointed_succ (f : â„• â†’ Î±) (n : â„•) : disjointed f (n + 1) = f (n + 1) \ partialSups f n :=
  rfl
#align disjointed_succ disjointed_succ

theorem disjointed_le_id : disjointed â‰¤ (id : (â„• â†’ Î±) â†’ â„• â†’ Î±) := by
  rintro f n
  -- âŠ¢ disjointed f n â‰¤ id f n
  cases n
  -- âŠ¢ disjointed f Nat.zero â‰¤ id f Nat.zero
  Â· rfl
    -- ğŸ‰ no goals
  Â· exact sdiff_le
    -- ğŸ‰ no goals
#align disjointed_le_id disjointed_le_id

theorem disjointed_le (f : â„• â†’ Î±) : disjointed f â‰¤ f :=
  disjointed_le_id f
#align disjointed_le disjointed_le

theorem disjoint_disjointed (f : â„• â†’ Î±) : Pairwise (Disjoint on disjointed f) := by
  refine' (Symmetric.pairwise_on Disjoint.symm _).2 fun m n h => _
  -- âŠ¢ Disjoint (disjointed f m) (disjointed f n)
  cases n
  -- âŠ¢ Disjoint (disjointed f m) (disjointed f Nat.zero)
  Â· exact (Nat.not_lt_zero _ h).elim
    -- ğŸ‰ no goals
  exact
    disjoint_sdiff_self_right.mono_left
      ((disjointed_le f m).trans (le_partialSups_of_le f (Nat.lt_add_one_iff.1 h)))
#align disjoint_disjointed disjoint_disjointed

-- Porting note: `disjointedRec` had a change in universe level.
/-- An induction principle for `disjointed`. To define/prove something on `disjointed f n`, it's
enough to define/prove it for `f n` and being able to extend through diffs. -/
def disjointedRec {f : â„• â†’ Î±} {p : Î± â†’ Sort*} (hdiff : âˆ€ â¦ƒt iâ¦„, p t â†’ p (t \ f i)) :
    âˆ€ â¦ƒnâ¦„, p (f n) â†’ p (disjointed f n)
  | 0 => id
  | n + 1 => fun h => by
    suffices H : âˆ€ k, p (f (n + 1) \ partialSups f k)
    -- âŠ¢ p (disjointed f (n + 1))
    Â· exact H n
      -- ğŸ‰ no goals
    rintro k
    -- âŠ¢ p (f (n + 1) \ â†‘(partialSups f) k)
    induction' k with k ih
    -- âŠ¢ p (f (n + 1) \ â†‘(partialSups f) Nat.zero)
    Â· exact hdiff h
      -- ğŸ‰ no goals
    rw [partialSups_succ, â† sdiff_sdiff_left]
    -- âŠ¢ p ((f (n + 1) \ â†‘(partialSups f) k) \ f (k + 1))
    exact hdiff ih
    -- ğŸ‰ no goals
#align disjointed_rec disjointedRec

@[simp]
theorem disjointedRec_zero {f : â„• â†’ Î±} {p : Î± â†’ Sort*} (hdiff : âˆ€ â¦ƒt iâ¦„, p t â†’ p (t \ f i))
    (hâ‚€ : p (f 0)) : disjointedRec hdiff hâ‚€ = hâ‚€ :=
  rfl
#align disjointed_rec_zero disjointedRec_zero

-- TODO: Find a useful statement of `disjointedRec_succ`.
theorem Monotone.disjointed_eq {f : â„• â†’ Î±} (hf : Monotone f) (n : â„•) :
    disjointed f (n + 1) = f (n + 1) \ f n := by rw [disjointed_succ, hf.partialSups_eq]
                                                 -- ğŸ‰ no goals
#align monotone.disjointed_eq Monotone.disjointed_eq

@[simp]
theorem partialSups_disjointed (f : â„• â†’ Î±) : partialSups (disjointed f) = partialSups f := by
  ext n
  -- âŠ¢ â†‘(partialSups (disjointed f)) n = â†‘(partialSups f) n
  induction' n with k ih
  -- âŠ¢ â†‘(partialSups (disjointed f)) Nat.zero = â†‘(partialSups f) Nat.zero
  Â· rw [partialSups_zero, partialSups_zero, disjointed_zero]
    -- ğŸ‰ no goals
  Â· rw [partialSups_succ, partialSups_succ, disjointed_succ, ih, sup_sdiff_self_right]
    -- ğŸ‰ no goals
#align partial_sups_disjointed partialSups_disjointed

/-- `disjointed f` is the unique sequence that is pairwise disjoint and has the same partial sups
as `f`. -/
theorem disjointed_unique {f d : â„• â†’ Î±} (hdisj : Pairwise (Disjoint on d))
    (hsups : partialSups d = partialSups f) : d = disjointed f := by
  ext n
  -- âŠ¢ d n = disjointed f n
  cases' n with n
  -- âŠ¢ d Nat.zero = disjointed f Nat.zero
  Â· rw [â† partialSups_zero d, hsups, partialSups_zero, disjointed_zero]
    -- ğŸ‰ no goals
  suffices h : d n.succ = partialSups d n.succ \ partialSups d n
  -- âŠ¢ d (Nat.succ n) = disjointed f (Nat.succ n)
  Â· rw [h, hsups, partialSups_succ, disjointed_succ, sup_sdiff, sdiff_self, bot_sup_eq]
    -- ğŸ‰ no goals
  rw [partialSups_succ, sup_sdiff, sdiff_self, bot_sup_eq, eq_comm, sdiff_eq_self_iff_disjoint]
  -- âŠ¢ Disjoint (â†‘(partialSups d) n) (d (n + 1))
  suffices h : âˆ€ m â‰¤ n, Disjoint (partialSups d m) (d n.succ)
  -- âŠ¢ Disjoint (â†‘(partialSups d) n) (d (n + 1))
  Â· exact h n le_rfl
    -- ğŸ‰ no goals
  rintro m hm
  -- âŠ¢ Disjoint (â†‘(partialSups d) m) (d (Nat.succ n))
  induction' m with m ih
  -- âŠ¢ Disjoint (â†‘(partialSups d) Nat.zero) (d (Nat.succ n))
  Â· exact hdisj (Nat.succ_ne_zero _).symm
    -- ğŸ‰ no goals
  rw [partialSups_succ, disjoint_iff, inf_sup_right, sup_eq_bot_iff, â† disjoint_iff, â† disjoint_iff]
  -- âŠ¢ Disjoint (â†‘(partialSups d) m) (d (Nat.succ n)) âˆ§ Disjoint (d (m + 1)) (d (Na â€¦
  exact âŸ¨ih (Nat.le_of_succ_le hm), hdisj (Nat.lt_succ_of_le hm).neâŸ©
  -- ğŸ‰ no goals
#align disjointed_unique disjointed_unique

end GeneralizedBooleanAlgebra

section CompleteBooleanAlgebra

variable [CompleteBooleanAlgebra Î±]

theorem iSup_disjointed (f : â„• â†’ Î±) : â¨† n, disjointed f n = â¨† n, f n :=
  iSup_eq_iSup_of_partialSups_eq_partialSups (partialSups_disjointed f)
#align supr_disjointed iSup_disjointed

theorem disjointed_eq_inf_compl (f : â„• â†’ Î±) (n : â„•) : disjointed f n = f n âŠ“ â¨… i < n, (f i)á¶œ := by
  cases n
  -- âŠ¢ disjointed f Nat.zero = f Nat.zero âŠ“ â¨… (i : â„•) (_ : i < Nat.zero), (f i)á¶œ
  Â· rw [disjointed_zero, eq_comm, inf_eq_left]
    -- âŠ¢ f Nat.zero â‰¤ â¨… (i : â„•) (_ : i < Nat.zero), (f i)á¶œ
    simp_rw [le_iInf_iff]
    -- âŠ¢ âˆ€ (i : â„•), i < Nat.zero â†’ f Nat.zero â‰¤ (f i)á¶œ
    exact fun i hi => (i.not_lt_zero hi).elim
    -- ğŸ‰ no goals
  simp_rw [disjointed_succ, partialSups_eq_biSup, sdiff_eq, compl_iSup]
  -- âŠ¢ f (nâœ + 1) âŠ“ â¨… (i : â„•) (_ : i â‰¤ nâœ), (f i)á¶œ = f (Nat.succ nâœ) âŠ“ â¨… (i : â„•) (_ â€¦
  congr
  -- âŠ¢ (fun i => â¨… (_ : i â‰¤ nâœ), (f i)á¶œ) = fun i => â¨… (_ : i < Nat.succ nâœ), (f i)á¶œ
  ext i
  -- âŠ¢ â¨… (_ : i â‰¤ nâœ), (f i)á¶œ = â¨… (_ : i < Nat.succ nâœ), (f i)á¶œ
  rw [Nat.lt_succ_iff]
  -- ğŸ‰ no goals
#align disjointed_eq_inf_compl disjointed_eq_inf_compl

end CompleteBooleanAlgebra

/-! ### Set notation variants of lemmas -/


theorem disjointed_subset (f : â„• â†’ Set Î±) (n : â„•) : disjointed f n âŠ† f n :=
  disjointed_le f n
#align disjointed_subset disjointed_subset

theorem iUnion_disjointed {f : â„• â†’ Set Î±} : â‹ƒ n, disjointed f n = â‹ƒ n, f n :=
  iSup_disjointed f
#align Union_disjointed iUnion_disjointed

theorem disjointed_eq_inter_compl (f : â„• â†’ Set Î±) (n : â„•) :
    disjointed f n = f n âˆ© â‹‚ i < n, (f i)á¶œ :=
  disjointed_eq_inf_compl f n
#align disjointed_eq_inter_compl disjointed_eq_inter_compl

theorem preimage_find_eq_disjointed (s : â„• â†’ Set Î±) (H : âˆ€ x, âˆƒ n, x âˆˆ s n)
    [âˆ€ x n, Decidable (x âˆˆ s n)] (n : â„•) : (fun x => Nat.find (H x)) â»Â¹' {n} = disjointed s n := by
  ext x
  -- âŠ¢ x âˆˆ (fun x => Nat.find (_ : âˆƒ n, x âˆˆ s n)) â»Â¹' {n} â†” x âˆˆ disjointed s n
  simp [Nat.find_eq_iff, disjointed_eq_inter_compl]
  -- ğŸ‰ no goals
#align preimage_find_eq_disjointed preimage_find_eq_disjointed
