/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Order.Interval.Set.OrdConnected
import Mathlib.Order.Filter.SmallSets
import Mathlib.Order.Filter.AtTopBot

/-!
# Convergence of intervals

## Motivation

If a function tends to infinity somewhere, then its derivative is not integrable around this place.
One should be careful about this statement: "somewhere" could mean a point, but also convergence
from the left or from the right, or it could also be infinity, and "around this place" will refer
to these directed neighborhoods. Therefore, the above theorem has many variants. Instead of stating
all these variants, one can look for the common abstraction and have a single version. One has to
be careful: if one considers convergence along a sequence, then the function may tend to infinity
but have a derivative which is small along the sequence (with big jumps in between), so in the end
the derivative may be integrable on a neighborhood of the sequence. What really matters for such
calculus issues in terms of derivatives is that whole intervals are included in the sets we
consider.

The right common abstraction is provided in this file, as the `TendstoIxxClass` typeclass.
It takes as parameters a class of bounded intervals and two real filters `lâ‚` and `lâ‚‚`.
An instance `TendstoIxxClass Icc lâ‚ lâ‚‚` registers that, if `aâ‚™` and `bâ‚™` are converging towards
the filter `lâ‚`, then the intervals `Icc aâ‚™ bâ‚™` are eventually contained in any given set
belonging to `lâ‚‚`. For instance, for `lâ‚ = ğ“[>] x` and `lâ‚‚ = ğ“[â‰¥] x`, the strict and large right
neighborhoods of `x` respectively, then given any large right neighborhood `s âˆˆ ğ“[â‰¥] x` and any two
sequences `xâ‚™` and `yâ‚™` converging strictly to the right of `x`,
then the interval `[xâ‚™, yâ‚™]` is eventually contained in `s`. Therefore, the instance
`TendstoIxxClass Icc (ğ“[>] x) (ğ“[â‰¥] x)` holds. Note that one could have taken as
well `lâ‚‚ = ğ“[>] x`, but that `lâ‚ = ğ“[â‰¥] x` and `lâ‚‚ = ğ“[>] x` wouldn't work.

With this formalism, the above theorem would read: if `TendstoIxxClass Icc l l` and `f` tends
to infinity along `l`, then its derivative is not integrable on any element of `l`.
Beyond this simple example, this typeclass plays a prominent role in generic formulations of
the fundamental theorem of calculus.

## Main definition

If both `a` and `b` tend to some filter `lâ‚`, sometimes this implies that `Ixx a b` tends to
`lâ‚‚.smallSets`, i.e., for any `s âˆˆ lâ‚‚` eventually `Ixx a b` becomes a subset of `s`. Here and below
`Ixx` is one of `Set.Icc`, `Set.Ico`, `Set.Ioc`, and `Set.Ioo`.
We define `Filter.TendstoIxxClass Ixx lâ‚ lâ‚‚` to be a typeclass representing this property.

The instances provide the best `lâ‚‚` for a given `lâ‚`. In many cases `lâ‚ = lâ‚‚` but sometimes we can
drop an endpoint from an interval: e.g., we prove
`Filter.TendstoIxxClass Set.Ico (ğ“Ÿ (Set.Iic a)) (ğ“Ÿ (Set.Iio a))`, i.e., if `uâ‚ n` and `uâ‚‚ n` belong
eventually to `Set.Iic a`, then the interval `Set.Ico (uâ‚ n) (uâ‚‚ n)` is eventually included in
`Set.Iio a`.

The next table shows â€œoutputâ€ filters `lâ‚‚` for different values of `Ixx` and `lâ‚`. The instances
that need topology are defined in `Mathlib/Topology/Algebra/Ordered`.

|     Input filter | `Ixx = Set.Icc`  | `Ixx = Set.Ico`  | `Ixx = Set.Ioc`  | `Ixx = Set.Ioo`  |
|-----------------:|:----------------:|:----------------:|:----------------:|:----------------:|
|   `Filter.atTop` | `Filter.atTop`   | `Filter.atTop`   | `Filter.atTop`   | `Filter.atTop`   |
|   `Filter.atBot` | `Filter.atBot`   | `Filter.atBot`   | `Filter.atBot`   | `Filter.atBot`   |
|         `pure a` | `pure a`         | `âŠ¥`              | `âŠ¥`              | `âŠ¥`              |
|  `ğ“Ÿ (Set.Iic a)` | `ğ“Ÿ (Set.Iic a)`  | `ğ“Ÿ (Set.Iio a)`  | `ğ“Ÿ (Set.Iic a)`  | `ğ“Ÿ (Set.Iio a)`  |
|  `ğ“Ÿ (Set.Ici a)` | `ğ“Ÿ (Set.Ici a)`  | `ğ“Ÿ (Set.Ici a)`  | `ğ“Ÿ (Set.Ioi a)`  | `ğ“Ÿ (Set.Ioi a)`  |
|  `ğ“Ÿ (Set.Ioi a)` | `ğ“Ÿ (Set.Ioi a)`  | `ğ“Ÿ (Set.Ioi a)`  | `ğ“Ÿ (Set.Ioi a)`  | `ğ“Ÿ (Set.Ioi a)`  |
|  `ğ“Ÿ (Set.Iio a)` | `ğ“Ÿ (Set.Iio a)`  | `ğ“Ÿ (Set.Iio a)`  | `ğ“Ÿ (Set.Iio a)`  | `ğ“Ÿ (Set.Iio a)`  |
|            `ğ“ a` | `ğ“ a`            | `ğ“ a`            | `ğ“ a`            | `ğ“ a`            |
| `ğ“[Set.Iic a] b` | `ğ“[Set.Iic a] b` | `ğ“[Set.Iio a] b` | `ğ“[Set.Iic a] b` | `ğ“[Set.Iio a] b` |
| `ğ“[Set.Ici a] b` | `ğ“[Set.Ici a] b` | `ğ“[Set.Ici a] b` | `ğ“[Set.Ioi a] b` | `ğ“[Set.Ioi a] b` |
| `ğ“[Set.Ioi a] b` | `ğ“[Set.Ioi a] b` | `ğ“[Set.Ioi a] b` | `ğ“[Set.Ioi a] b` | `ğ“[Set.Ioi a] b` |
| `ğ“[Set.Iio a] b` | `ğ“[Set.Iio a] b` | `ğ“[Set.Iio a] b` | `ğ“[Set.Iio a] b` | `ğ“[Set.Iio a] b` |

-/


variable {Î± Î² : Type*}

open Filter Set Function

namespace Filter

section Preorder

/-- A pair of filters `lâ‚`, `lâ‚‚` has `TendstoIxxClass Ixx` property if `Ixx a b` tends to
`lâ‚‚.small_sets` as `a` and `b` tend to `lâ‚`. In all instances `Ixx` is one of `Set.Icc`, `Set.Ico`,
`Set.Ioc`, or `Set.Ioo`. The instances provide the best `lâ‚‚` for a given `lâ‚`. In many cases
`lâ‚ = lâ‚‚` but sometimes we can drop an endpoint from an interval: e.g., we prove
`TendstoIxxClass Set.Ico (ğ“Ÿ (Set.Iic a)) (ğ“Ÿ (Set.Iio a))`, i.e., if `uâ‚ n` and `uâ‚‚ n` belong
eventually to `Set.Iic a`, then the interval `Set.Ico (uâ‚ n) (uâ‚‚ n)` is eventually included in
`Set.Iio a`.

We mark `lâ‚‚` as an `outParam` so that Lean can automatically find an appropriate `lâ‚‚` based on
`Ixx` and `lâ‚`. This way, e.g., `tendsto.Ico hâ‚ hâ‚‚` works without specifying explicitly `lâ‚‚`. -/
class TendstoIxxClass (Ixx : Î± â†’ Î± â†’ Set Î±) (lâ‚ : Filter Î±) (lâ‚‚ : outParam <| Filter Î±) : Prop where
  /-- `Function.uncurry Ixx` tends to `lâ‚‚.smallSets` along `lâ‚ Ã—Ë¢ lâ‚`. In other words, for any
  `s âˆˆ lâ‚‚` there exists `t âˆˆ lâ‚` such that `Ixx x y âŠ† s` whenever `x âˆˆ t` and `y âˆˆ t`.

  Use lemmas like `Filter.Tendsto.Icc` instead. -/
  tendsto_Ixx : Tendsto (fun p : Î± Ã— Î± => Ixx p.1 p.2) (lâ‚ Ã—Ë¢ lâ‚) lâ‚‚.smallSets

theorem tendstoIxxClass_principal {s t : Set Î±} {Ixx : Î± â†’ Î± â†’ Set Î±} :
    TendstoIxxClass Ixx (ğ“Ÿ s) (ğ“Ÿ t) â†” âˆ€áµ‰ (x âˆˆ s) (y âˆˆ s), Ixx x y âŠ† t :=
  Iff.trans âŸ¨fun h => h.1, fun h => âŸ¨hâŸ©âŸ© <| by
    simp only [smallSets_principal, prod_principal_principal, tendsto_principal_principal,
      forall_prod_set, mem_powerset_iff, mem_principal]

theorem tendstoIxxClass_inf {lâ‚ lâ‚' lâ‚‚ lâ‚‚' : Filter Î±} {Ixx} [h : TendstoIxxClass Ixx lâ‚ lâ‚‚]
    [h' : TendstoIxxClass Ixx lâ‚' lâ‚‚'] : TendstoIxxClass Ixx (lâ‚ âŠ“ lâ‚') (lâ‚‚ âŠ“ lâ‚‚') :=
  âŸ¨by simpa only [prod_inf_prod, smallSets_inf] using h.1.inf h'.1âŸ©

theorem tendstoIxxClass_of_subset {lâ‚ lâ‚‚ : Filter Î±} {Ixx Ixx' : Î± â†’ Î± â†’ Set Î±}
    (h : âˆ€ a b, Ixx a b âŠ† Ixx' a b) [h' : TendstoIxxClass Ixx' lâ‚ lâ‚‚] : TendstoIxxClass Ixx lâ‚ lâ‚‚ :=
  âŸ¨h'.1.smallSets_mono <| Eventually.of_forall <| Prod.forall.2 hâŸ©

theorem HasBasis.tendstoIxxClass {Î¹ : Type*} {p : Î¹ â†’ Prop} {s} {l : Filter Î±}
    (hl : l.HasBasis p s) {Ixx : Î± â†’ Î± â†’ Set Î±}
    (H : âˆ€ i, p i â†’ âˆ€ x âˆˆ s i, âˆ€ y âˆˆ s i, Ixx x y âŠ† s i) : TendstoIxxClass Ixx l l :=
  âŸ¨(hl.prod_self.tendsto_iff hl.smallSets).2 fun i hi => âŸ¨i, hi, fun _ h => H i hi _ h.1 _ h.2âŸ©âŸ©

variable [Preorder Î±]

protected theorem Tendsto.Icc {lâ‚ lâ‚‚ : Filter Î±} [TendstoIxxClass Icc lâ‚ lâ‚‚] {lb : Filter Î²}
    {uâ‚ uâ‚‚ : Î² â†’ Î±} (hâ‚ : Tendsto uâ‚ lb lâ‚) (hâ‚‚ : Tendsto uâ‚‚ lb lâ‚) :
    Tendsto (fun x => Icc (uâ‚ x) (uâ‚‚ x)) lb lâ‚‚.smallSets :=
  (@TendstoIxxClass.tendsto_Ixx Î± Set.Icc _ _ _).comp <| hâ‚.prod_mk hâ‚‚

protected theorem Tendsto.Ioc {lâ‚ lâ‚‚ : Filter Î±} [TendstoIxxClass Ioc lâ‚ lâ‚‚] {lb : Filter Î²}
    {uâ‚ uâ‚‚ : Î² â†’ Î±} (hâ‚ : Tendsto uâ‚ lb lâ‚) (hâ‚‚ : Tendsto uâ‚‚ lb lâ‚) :
    Tendsto (fun x => Ioc (uâ‚ x) (uâ‚‚ x)) lb lâ‚‚.smallSets :=
  (@TendstoIxxClass.tendsto_Ixx Î± Set.Ioc _ _ _).comp <| hâ‚.prod_mk hâ‚‚

protected theorem Tendsto.Ico {lâ‚ lâ‚‚ : Filter Î±} [TendstoIxxClass Ico lâ‚ lâ‚‚] {lb : Filter Î²}
    {uâ‚ uâ‚‚ : Î² â†’ Î±} (hâ‚ : Tendsto uâ‚ lb lâ‚) (hâ‚‚ : Tendsto uâ‚‚ lb lâ‚) :
    Tendsto (fun x => Ico (uâ‚ x) (uâ‚‚ x)) lb lâ‚‚.smallSets :=
  (@TendstoIxxClass.tendsto_Ixx Î± Set.Ico _ _ _).comp <| hâ‚.prod_mk hâ‚‚

protected theorem Tendsto.Ioo {lâ‚ lâ‚‚ : Filter Î±} [TendstoIxxClass Ioo lâ‚ lâ‚‚] {lb : Filter Î²}
    {uâ‚ uâ‚‚ : Î² â†’ Î±} (hâ‚ : Tendsto uâ‚ lb lâ‚) (hâ‚‚ : Tendsto uâ‚‚ lb lâ‚) :
    Tendsto (fun x => Ioo (uâ‚ x) (uâ‚‚ x)) lb lâ‚‚.smallSets :=
  (@TendstoIxxClass.tendsto_Ixx Î± Set.Ioo _ _ _).comp <| hâ‚.prod_mk hâ‚‚


instance tendsto_Icc_atTop_atTop : TendstoIxxClass Icc (atTop : Filter Î±) atTop :=
  (hasBasis_iInf_principal_finite _).tendstoIxxClass fun _ _ =>
    Set.OrdConnected.out <| ordConnected_biInter fun _ _ => ordConnected_Ici

instance tendsto_Ico_atTop_atTop : TendstoIxxClass Ico (atTop : Filter Î±) atTop :=
  tendstoIxxClass_of_subset fun _ _ => Ico_subset_Icc_self

instance tendsto_Ioc_atTop_atTop : TendstoIxxClass Ioc (atTop : Filter Î±) atTop :=
  tendstoIxxClass_of_subset fun _ _ => Ioc_subset_Icc_self

instance tendsto_Ioo_atTop_atTop : TendstoIxxClass Ioo (atTop : Filter Î±) atTop :=
  tendstoIxxClass_of_subset fun _ _ => Ioo_subset_Icc_self

instance tendsto_Icc_atBot_atBot : TendstoIxxClass Icc (atBot : Filter Î±) atBot :=
  (hasBasis_iInf_principal_finite _).tendstoIxxClass fun _ _ =>
    Set.OrdConnected.out <| ordConnected_biInter fun _ _ => ordConnected_Iic

instance tendsto_Ico_atBot_atBot : TendstoIxxClass Ico (atBot : Filter Î±) atBot :=
  tendstoIxxClass_of_subset fun _ _ => Ico_subset_Icc_self

instance tendsto_Ioc_atBot_atBot : TendstoIxxClass Ioc (atBot : Filter Î±) atBot :=
  tendstoIxxClass_of_subset fun _ _ => Ioc_subset_Icc_self

instance tendsto_Ioo_atBot_atBot : TendstoIxxClass Ioo (atBot : Filter Î±) atBot :=
  tendstoIxxClass_of_subset fun _ _ => Ioo_subset_Icc_self

instance OrdConnected.tendsto_Icc {s : Set Î±} [hs : OrdConnected s] :
    TendstoIxxClass Icc (ğ“Ÿ s) (ğ“Ÿ s) :=
  tendstoIxxClass_principal.2 hs.out

instance tendsto_Ico_Ici_Ici {a : Î±} : TendstoIxxClass Ico (ğ“Ÿ (Ici a)) (ğ“Ÿ (Ici a)) :=
  tendstoIxxClass_of_subset fun _ _ => Ico_subset_Icc_self

instance tendsto_Ico_Ioi_Ioi {a : Î±} : TendstoIxxClass Ico (ğ“Ÿ (Ioi a)) (ğ“Ÿ (Ioi a)) :=
  tendstoIxxClass_of_subset fun _ _ => Ico_subset_Icc_self

instance tendsto_Ico_Iic_Iio {a : Î±} : TendstoIxxClass Ico (ğ“Ÿ (Iic a)) (ğ“Ÿ (Iio a)) :=
  tendstoIxxClass_principal.2 fun _ _ _ hâ‚ _ hâ‚‚ => lt_of_lt_of_le hâ‚‚.2 hâ‚

instance tendsto_Ico_Iio_Iio {a : Î±} : TendstoIxxClass Ico (ğ“Ÿ (Iio a)) (ğ“Ÿ (Iio a)) :=
  tendstoIxxClass_of_subset fun _ _ => Ico_subset_Icc_self

instance tendsto_Ioc_Ici_Ioi {a : Î±} : TendstoIxxClass Ioc (ğ“Ÿ (Ici a)) (ğ“Ÿ (Ioi a)) :=
  tendstoIxxClass_principal.2 fun _ hâ‚ _ _ _ hâ‚‚ => lt_of_le_of_lt hâ‚ hâ‚‚.1

instance tendsto_Ioc_Iic_Iic {a : Î±} : TendstoIxxClass Ioc (ğ“Ÿ (Iic a)) (ğ“Ÿ (Iic a)) :=
  tendstoIxxClass_of_subset fun _ _ => Ioc_subset_Icc_self

instance tendsto_Ioc_Iio_Iio {a : Î±} : TendstoIxxClass Ioc (ğ“Ÿ (Iio a)) (ğ“Ÿ (Iio a)) :=
  tendstoIxxClass_of_subset fun _ _ => Ioc_subset_Icc_self

instance tendsto_Ioc_Ioi_Ioi {a : Î±} : TendstoIxxClass Ioc (ğ“Ÿ (Ioi a)) (ğ“Ÿ (Ioi a)) :=
  tendstoIxxClass_of_subset fun _ _ => Ioc_subset_Icc_self

instance tendsto_Ioo_Ici_Ioi {a : Î±} : TendstoIxxClass Ioo (ğ“Ÿ (Ici a)) (ğ“Ÿ (Ioi a)) :=
  tendstoIxxClass_of_subset fun _ _ => Ioo_subset_Ioc_self

instance tendsto_Ioo_Iic_Iio {a : Î±} : TendstoIxxClass Ioo (ğ“Ÿ (Iic a)) (ğ“Ÿ (Iio a)) :=
  tendstoIxxClass_of_subset fun _ _ => Ioo_subset_Ico_self

instance tendsto_Ioo_Ioi_Ioi {a : Î±} : TendstoIxxClass Ioo (ğ“Ÿ (Ioi a)) (ğ“Ÿ (Ioi a)) :=
  tendstoIxxClass_of_subset fun _ _ => Ioo_subset_Ioc_self

instance tendsto_Ioo_Iio_Iio {a : Î±} : TendstoIxxClass Ioo (ğ“Ÿ (Iio a)) (ğ“Ÿ (Iio a)) :=
  tendstoIxxClass_of_subset fun _ _ => Ioo_subset_Ioc_self

instance tendsto_Icc_Icc_Icc {a b : Î±} : TendstoIxxClass Icc (ğ“Ÿ (Icc a b)) (ğ“Ÿ (Icc a b)) :=
  tendstoIxxClass_principal.mpr fun _x hx _y hy => Icc_subset_Icc hx.1 hy.2

instance tendsto_Ioc_Icc_Icc {a b : Î±} : TendstoIxxClass Ioc (ğ“Ÿ (Icc a b)) (ğ“Ÿ (Icc a b)) :=
  tendstoIxxClass_of_subset fun _ _ => Ioc_subset_Icc_self

end Preorder

section PartialOrder

variable [PartialOrder Î±]

instance tendsto_Icc_pure_pure {a : Î±} : TendstoIxxClass Icc (pure a) (pure a : Filter Î±) := by
  rw [â† principal_singleton]
  exact tendstoIxxClass_principal.2 ordConnected_singleton.out

instance tendsto_Ico_pure_bot {a : Î±} : TendstoIxxClass Ico (pure a) âŠ¥ :=
  âŸ¨by simpâŸ©

instance tendsto_Ioc_pure_bot {a : Î±} : TendstoIxxClass Ioc (pure a) âŠ¥ :=
  âŸ¨by simpâŸ©

instance tendsto_Ioo_pure_bot {a : Î±} : TendstoIxxClass Ioo (pure a) âŠ¥ :=
  âŸ¨by simpâŸ©

end PartialOrder

section LinearOrder

open Interval

variable [LinearOrder Î±]

instance tendsto_Icc_uIcc_uIcc {a b : Î±} : TendstoIxxClass Icc (ğ“Ÿ [[a, b]]) (ğ“Ÿ [[a, b]]) :=
  Filter.tendsto_Icc_Icc_Icc

instance tendsto_Ioc_uIcc_uIcc {a b : Î±} : TendstoIxxClass Ioc (ğ“Ÿ [[a, b]]) (ğ“Ÿ [[a, b]]) :=
  Filter.tendsto_Ioc_Icc_Icc

instance tendsto_uIcc_of_Icc {l : Filter Î±} [TendstoIxxClass Icc l l] :
    TendstoIxxClass uIcc l l := by
  refine âŸ¨fun s hs => mem_map.2 <| mem_prod_self_iff.2 ?_âŸ©
  obtain âŸ¨t, htl, htsâŸ© : âˆƒ t âˆˆ l, âˆ€ p âˆˆ (t : Set Î±) Ã—Ë¢ t, Icc (p : Î± Ã— Î±).1 p.2 âˆˆ s :=
    mem_prod_self_iff.1 (mem_map.1 (tendsto_fst.Icc tendsto_snd hs))
  refine âŸ¨t, htl, fun p hp => ?_âŸ©
  rcases le_total p.1 p.2 with h | h
  Â· rw [mem_preimage, uIcc_of_le h]
    exact hts p hp
  Â· rw [mem_preimage, uIcc_of_ge h]
    exact hts âŸ¨p.2, p.1âŸ© âŸ¨hp.2, hp.1âŸ©

protected theorem Tendsto.uIcc {l : Filter Î±} [TendstoIxxClass Icc l l] {f g : Î² â†’ Î±}
    {lb : Filter Î²} (hf : Tendsto f lb l) (hg : Tendsto g lb l) :
    Tendsto (fun x => [[f x, g x]]) lb l.smallSets :=
  (@TendstoIxxClass.tendsto_Ixx Î± Set.uIcc _ _ _).comp <| hf.prod_mk hg

end LinearOrder

end Filter
