/-
Copyright (c) 2023 Yury Kudryashov All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Order.Filter.CountableInter

/-!
# Filters with countable intersections and countable separating families

In this file we prove some facts about a filter with countable intersections property on a type with
a countable family of sets that separates points of the space. The main use case is the
`MeasureTheory.Measure.ae` filter and a space with countably generated Ïƒ-algebra but lemmas apply,
e.g., to the `residual` filter and a Tâ‚€ topological space with second countable topology.

To avoid repetition of lemmas for different families of separating sets (measurable sets, open sets,
closed sets), all theorems in this file take a predicate `p : Set Î± â†’ Prop` as an argument and prove
existence of a countable separating family satisfying this predicate by searching for a
`HasCountableSeparatingOn` typeclass instance.

## Main definitions

- `HasCountableSeparatingOn Î± p t`: a typeclass saying that there exists a countable set family
  `S : Set (Set Î±)` such that all `s âˆˆ S` satisfy the predicate `p` and any two distinct points
  `x y âˆˆ t`, `x â‰  y`, can be separated by a set `s âˆˆ S`. For technical reasons, we formulate the
  latter property as "for all `x y âˆˆ t`, if `x âˆˆ s â†” y âˆˆ s` for all `s âˆˆ S`, then `x = y`".

This typeclass is used in all lemmas in this file to avoid repeating them for open sets, closed
sets, and measurable sets.

### Main results

#### Filters supported on a (sub)singleton

Let `l : Filter Î±` be a filter with countable intersections property. Let `p : Set Î± â†’ Prop` be a
property such that there exists a countable family of sets satisfying `p` and separating points of
`Î±`. Then `l` is supported on a subsingleton: there exists a subsingleton `t` such that
`t âˆˆ l`.

We formalize various versions of this theorem in
`Filter.exists_subset_subsingleton_mem_of_forall_separating`,
`Filter.exists_mem_singleton_mem_of_mem_of_nonempty_of_forall_separating`,
`Filter.exists_singleton_mem_of_mem_of_forall_separating`,
`Filter.exists_subsingleton_mem_of_forall_separating`, and
`Filter.exists_singleton_mem_of_forall_separating`.

#### Eventually constant functions

Consider a function `f : Î± â†’ Î²`, a filter `l` with countable intersections property, and a countable
separating family of sets of `Î²`. Suppose that for every `U` from the family, either
`âˆ€á¶  x in l, f x âˆˆ U` or `âˆ€á¶  x in l, f x âˆ‰ U`. Then `f` is eventually constant along `l`.

We formalize three versions of this theorem in
`Filter.exists_mem_eventuallyEq_const_of_eventually_mem_of_forall_separating`,
`Filter.exists_eventuallyEq_const_of_eventually_mem_of_forall_separating`, and
`Filer.exists_eventuallyEq_const_of_forall_separating`.

#### Eventually equal functions

Two functions are equal along a filter with countable intersections property if the preimages of all
sets from a countable separating family of sets are equal along the filter.

We formalize several versions of this theorem in
`Filter.of_eventually_mem_of_forall_separating_mem_iff`, `Filter.of_forall_separating_mem_iff`,
`Filter.of_eventually_mem_of_forall_separating_preimage`, and
`Filter.of_forall_separating_preimage`.

## Keywords

filter, countable
-/

set_option autoImplicit true

open Function Set Filter

/-- We say that a type `Î±` has a *countable separating family of sets* satisfying a predicate
`p : Set Î± â†’ Prop` on a set `t` if there exists a countable family of sets `S : Set (Set Î±)` such
that all sets `s âˆˆ S` satisfy `p` and any two distinct points `x y âˆˆ t`, `x â‰  y`, can be separated
by `s âˆˆ S`: there exists `s âˆˆ S` such that exactly one of `x` and `y` belongs to `s`.

E.g., if `Î±` is a `Tâ‚€` topological space with second countable topology, then it has a countable
separating family of open sets and a countable separating family of closed sets.
-/
class HasCountableSeparatingOn (Î± : Type*) (p : Set Î± â†’ Prop) (t : Set Î±) : Prop where
  exists_countable_separating : âˆƒ S : Set (Set Î±), S.Countable âˆ§ (âˆ€ s âˆˆ S, p s) âˆ§
    âˆ€ x âˆˆ t, âˆ€ y âˆˆ t, (âˆ€ s âˆˆ S, x âˆˆ s â†” y âˆˆ s) â†’ x = y

theorem exists_countable_separating (Î± : Type*) (p : Set Î± â†’ Prop) (t : Set Î±)
    [h : HasCountableSeparatingOn Î± p t] :
    âˆƒ S : Set (Set Î±), S.Countable âˆ§ (âˆ€ s âˆˆ S, p s) âˆ§
      âˆ€ x âˆˆ t, âˆ€ y âˆˆ t, (âˆ€ s âˆˆ S, x âˆˆ s â†” y âˆˆ s) â†’ x = y :=
  h.1

theorem exists_nonempty_countable_separating (Î± : Type*) {p : Set Î± â†’ Prop} {sâ‚€} (hp : p sâ‚€)
    (t : Set Î±) [HasCountableSeparatingOn Î± p t] :
    âˆƒ S : Set (Set Î±), S.Nonempty âˆ§ S.Countable âˆ§ (âˆ€ s âˆˆ S, p s) âˆ§
      âˆ€ x âˆˆ t, âˆ€ y âˆˆ t, (âˆ€ s âˆˆ S, x âˆˆ s â†” y âˆˆ s) â†’ x = y :=
  let âŸ¨S, hSc, hSp, hStâŸ© := exists_countable_separating Î± p t
  âŸ¨insert sâ‚€ S, insert_nonempty _ _, hSc.insert _, forall_insert_of_forall hSp hp,
    fun x hx y hy hxy â†¦ hSt x hx y hy <| forall_of_forall_insert hxyâŸ©

theorem exists_seq_separating (Î± : Type*) {p : Set Î± â†’ Prop} {sâ‚€} (hp : p sâ‚€) (t : Set Î±)
    [HasCountableSeparatingOn Î± p t] :
    âˆƒ S : â„• â†’ Set Î±, (âˆ€ n, p (S n)) âˆ§ âˆ€ x âˆˆ t, âˆ€ y âˆˆ t, (âˆ€ n, x âˆˆ S n â†” y âˆˆ S n) â†’ x = y := by
  rcases exists_nonempty_countable_separating Î± hp t with âŸ¨S, hSne, hSc, hSâŸ©
  -- âŠ¢ âˆƒ S, (âˆ€ (n : â„•), p (S n)) âˆ§ âˆ€ (x : Î±), x âˆˆ t â†’ âˆ€ (y : Î±), y âˆˆ t â†’ (âˆ€ (n : â„•) â€¦
  rcases hSc.exists_eq_range hSne with âŸ¨S, rflâŸ©
  -- âŠ¢ âˆƒ S, (âˆ€ (n : â„•), p (S n)) âˆ§ âˆ€ (x : Î±), x âˆˆ t â†’ âˆ€ (y : Î±), y âˆˆ t â†’ (âˆ€ (n : â„•) â€¦
  use S
  -- âŠ¢ (âˆ€ (n : â„•), p (S n)) âˆ§ âˆ€ (x : Î±), x âˆˆ t â†’ âˆ€ (y : Î±), y âˆˆ t â†’ (âˆ€ (n : â„•), x âˆˆ â€¦
  simpa only [forall_range_iff] using hS
  -- ğŸ‰ no goals

theorem HasCountableSeparatingOn.mono {Î±} {pâ‚ pâ‚‚ : Set Î± â†’ Prop} {tâ‚ tâ‚‚ : Set Î±}
    [h : HasCountableSeparatingOn Î± pâ‚ tâ‚] (hp : âˆ€ s, pâ‚ s â†’ pâ‚‚ s) (ht : tâ‚‚ âŠ† tâ‚) :
    HasCountableSeparatingOn Î± pâ‚‚ tâ‚‚ where
  exists_countable_separating :=
    let âŸ¨S, hSc, hSp, hStâŸ© := h.1
    âŸ¨S, hSc, fun s hs â†¦ hp s (hSp s hs), fun x hx y hy â†¦ hSt x (ht hx) y (ht hy)âŸ©

theorem HasCountableSeparatingOn.of_subtype {Î± : Type*} {p : Set Î± â†’ Prop} {t : Set Î±}
    {q : Set t â†’ Prop} [h : HasCountableSeparatingOn t q univ]
    (hpq : âˆ€ U, q U â†’ âˆƒ V, p V âˆ§ (â†‘) â»Â¹' V = U) : HasCountableSeparatingOn Î± p t := by
  rcases h.1 with âŸ¨S, hSc, hSq, hSâŸ©
  -- âŠ¢ HasCountableSeparatingOn Î± p t
  choose! V hpV hV using fun s hs â†¦ hpq s (hSq s hs)
  -- âŠ¢ HasCountableSeparatingOn Î± p t
  refine âŸ¨âŸ¨V '' S, hSc.image _, ball_image_iff.2 hpV, fun x hx y hy h â†¦ ?_âŸ©âŸ©
  -- âŠ¢ x = y
  refine congr_arg Subtype.val (hS âŸ¨x, hxâŸ© trivial âŸ¨y, hyâŸ© trivial fun U hU â†¦ ?_)
  -- âŠ¢ { val := x, property := hx } âˆˆ U â†” { val := y, property := hy } âˆˆ U
  rw [â† hV U hU]
  -- âŠ¢ { val := x, property := hx } âˆˆ Subtype.val â»Â¹' V U â†” { val := y, property := â€¦
  exact h _ (mem_image_of_mem _ hU)
  -- ğŸ‰ no goals

namespace Filter

variable {l : Filter Î±} [CountableInterFilter l] {f g : Î± â†’ Î²}

/-!
### Filters supported on a (sub)singleton

In this section we prove several versions of the following theorem. Let `l : Filter Î±` be a filter
with countable intersections property. Let `p : Set Î± â†’ Prop` be a property such that there exists a
countable family of sets satisfying `p` and separating points of `Î±`. Then `l` is supported on
a subsingleton: there exists a subsingleton `t` such that `t âˆˆ l`.

With extra `Nonempty`/`Set.Nonempty` assumptions one can ensure that `t` is a singleton `{x}`.

If `s âˆˆ l`, then it suffices to assume that the countable family separates only points of `s`.
-/

theorem exists_subset_subsingleton_mem_of_forall_separating (p : Set Î± â†’ Prop)
    {s : Set Î±} [h : HasCountableSeparatingOn Î± p s] (hs : s âˆˆ l)
    (hl : âˆ€ U, p U â†’ U âˆˆ l âˆ¨ Uá¶œ âˆˆ l) : âˆƒ t, t âŠ† s âˆ§ t.Subsingleton âˆ§ t âˆˆ l := by
  rcases h.1 with âŸ¨S, hSc, hSp, hSâŸ©
  -- âŠ¢ âˆƒ t, t âŠ† s âˆ§ Set.Subsingleton t âˆ§ t âˆˆ l
  refine âŸ¨s âˆ© â‹‚â‚€ (S âˆ© l.sets) âˆ© â‹‚ (U âˆˆ S) (_ : Uá¶œ âˆˆ l), Uá¶œ, ?_, ?_, ?_âŸ©
  Â· exact fun _ h â†¦ h.1.1
    -- ğŸ‰ no goals
  Â· intro x hx y hy
    -- âŠ¢ x = y
    simp only [mem_sInter, mem_inter_iff, mem_iInter, mem_compl_iff] at hx hy
    -- âŠ¢ x = y
    refine hS x hx.1.1 y hy.1.1 (fun s hsS â†¦ ?_)
    -- âŠ¢ x âˆˆ s â†” y âˆˆ s
    cases hl s (hSp s hsS) with
    | inl hsl => simp only [hx.1.2 s âŸ¨hsS, hslâŸ©, hy.1.2 s âŸ¨hsS, hslâŸ©]
    | inr hsl => simp only [hx.2 s hsS hsl, hy.2 s hsS hsl]
  Â· exact inter_mem
      (inter_mem hs ((countable_sInter_mem (hSc.mono (inter_subset_left _ _))).2 fun _ h â†¦ h.2))
      ((countable_bInter_mem hSc).2 fun U hU â†¦ iInter_mem.2 id)

theorem exists_mem_singleton_mem_of_mem_of_nonempty_of_forall_separating (p : Set Î± â†’ Prop)
    {s : Set Î±} [HasCountableSeparatingOn Î± p s] (hs : s âˆˆ l) (hne : s.Nonempty)
    (hl : âˆ€ U, p U â†’ U âˆˆ l âˆ¨ Uá¶œ âˆˆ l) : âˆƒ a âˆˆ s, {a} âˆˆ l := by
  rcases exists_subset_subsingleton_mem_of_forall_separating p hs hl with âŸ¨t, hts, ht, htlâŸ©
  -- âŠ¢ âˆƒ a, a âˆˆ s âˆ§ {a} âˆˆ l
  rcases ht.eq_empty_or_singleton with rfl | âŸ¨x, rflâŸ©
  -- âŠ¢ âˆƒ a, a âˆˆ s âˆ§ {a} âˆˆ l
  Â· exact hne.imp fun a ha â†¦ âŸ¨ha, mem_of_superset htl (empty_subset _)âŸ©
    -- ğŸ‰ no goals
  Â· exact âŸ¨x, hts rfl, htlâŸ©
    -- ğŸ‰ no goals

theorem exists_singleton_mem_of_mem_of_forall_separating [Nonempty Î±] (p : Set Î± â†’ Prop)
    {s : Set Î±} [HasCountableSeparatingOn Î± p s] (hs : s âˆˆ l) (hl : âˆ€ U, p U â†’ U âˆˆ l âˆ¨ Uá¶œ âˆˆ l) :
    âˆƒ a, {a} âˆˆ l := by
  rcases s.eq_empty_or_nonempty with rfl | hne
  -- âŠ¢ âˆƒ a, {a} âˆˆ l
  Â· exact â€¹Nonempty Î±â€º.elim fun a â†¦ âŸ¨a, mem_of_superset hs (empty_subset _)âŸ©
    -- ğŸ‰ no goals
  Â· exact (exists_mem_singleton_mem_of_mem_of_nonempty_of_forall_separating p hs hne hl).imp fun _ â†¦
      And.right

theorem exists_subsingleton_mem_of_forall_separating (p : Set Î± â†’ Prop)
    [HasCountableSeparatingOn Î± p univ] (hl : âˆ€ U, p U â†’ U âˆˆ l âˆ¨ Uá¶œ âˆˆ l) :
    âˆƒ s : Set Î±, s.Subsingleton âˆ§ s âˆˆ l :=
  let âŸ¨t, _, hts, htlâŸ© := exists_subset_subsingleton_mem_of_forall_separating p univ_mem hl
  âŸ¨t, hts, htlâŸ©

theorem exists_singleton_mem_of_forall_separating [Nonempty Î±] (p : Set Î± â†’ Prop)
    [HasCountableSeparatingOn Î± p univ] (hl : âˆ€ U, p U â†’ U âˆˆ l âˆ¨ Uá¶œ âˆˆ l) :
    âˆƒ x : Î±, {x} âˆˆ l :=
  exists_singleton_mem_of_mem_of_forall_separating p univ_mem hl

/-!
### Eventually constant functions

In this section we apply theorems from the previous section to the filter `Filter.map f l` to show
that `f : Î± â†’ Î²` is eventually constant along `l` if for every `U` from the separating family,
either `âˆ€á¶  x in l, f x âˆˆ U` or `âˆ€á¶  x in l, f x âˆ‰ U`.
-/

theorem exists_mem_eventuallyEq_const_of_eventually_mem_of_forall_separating (p : Set Î² â†’ Prop)
    {s : Set Î²} [HasCountableSeparatingOn Î² p s] (hs : âˆ€á¶  x in l, f x âˆˆ s) (hne : s.Nonempty)
    (h : âˆ€ U, p U â†’ (âˆ€á¶  x in l, f x âˆˆ U) âˆ¨ (âˆ€á¶  x in l, f x âˆ‰ U)) :
    âˆƒ a âˆˆ s, f =á¶ [l] const Î± a :=
  exists_mem_singleton_mem_of_mem_of_nonempty_of_forall_separating p (l := map f l) hs hne h

theorem exists_eventuallyEq_const_of_eventually_mem_of_forall_separating [Nonempty Î²]
    (p : Set Î² â†’ Prop) {s : Set Î²} [HasCountableSeparatingOn Î² p s] (hs : âˆ€á¶  x in l, f x âˆˆ s)
    (h : âˆ€ U, p U â†’ (âˆ€á¶  x in l, f x âˆˆ U) âˆ¨ (âˆ€á¶  x in l, f x âˆ‰ U)) :
    âˆƒ a, f =á¶ [l] const Î± a :=
  exists_singleton_mem_of_mem_of_forall_separating (l := map f l) p hs h

theorem exists_eventuallyEq_const_of_forall_separating [Nonempty Î²] (p : Set Î² â†’ Prop)
    [HasCountableSeparatingOn Î² p univ]
    (h : âˆ€ U, p U â†’ (âˆ€á¶  x in l, f x âˆˆ U) âˆ¨ (âˆ€á¶  x in l, f x âˆ‰ U)) :
    âˆƒ a, f =á¶ [l] const Î± a :=
  exists_singleton_mem_of_forall_separating (l := map f l) p h

namespace EventuallyEq

/-!
### Eventually equal functions

In this section we show that two functions are equal along a filter with countable intersections
property if the preimages of all sets from a countable separating family of sets are equal along
the filter.
-/

theorem of_eventually_mem_of_forall_separating_mem_iff (p : Set Î² â†’ Prop) {s : Set Î²}
    [h' : HasCountableSeparatingOn Î² p s] (hf : âˆ€á¶  x in l, f x âˆˆ s) (hg : âˆ€á¶  x in l, g x âˆˆ s)
    (h : âˆ€ U : Set Î², p U â†’ âˆ€á¶  x in l, f x âˆˆ U â†” g x âˆˆ U) : f =á¶ [l] g := by
  rcases h'.1 with âŸ¨S, hSc, hSp, hSâŸ©
  -- âŠ¢ f =á¶ [l] g
  have H : âˆ€á¶  x in l, âˆ€ s âˆˆ S, f x âˆˆ s â†” g x âˆˆ s :=
    (eventually_countable_ball hSc).2 fun s hs â†¦ (h _ (hSp _ hs))
  filter_upwards [H, hf, hg] with x hx hxf hxg using hS _ hxf _ hxg hx
  -- ğŸ‰ no goals

theorem of_forall_separating_mem_iff (p : Set Î² â†’ Prop)
    [HasCountableSeparatingOn Î² p univ] (h : âˆ€ U : Set Î², p U â†’ âˆ€á¶  x in l, f x âˆˆ U â†” g x âˆˆ U) :
    f =á¶ [l] g :=
  of_eventually_mem_of_forall_separating_mem_iff p (s := univ) univ_mem univ_mem h

theorem of_eventually_mem_of_forall_separating_preimage (p : Set Î² â†’ Prop) {s : Set Î²}
    [HasCountableSeparatingOn Î² p s] (hf : âˆ€á¶  x in l, f x âˆˆ s) (hg : âˆ€á¶  x in l, g x âˆˆ s)
    (h : âˆ€ U : Set Î², p U â†’ f â»Â¹' U =á¶ [l] g â»Â¹' U) : f =á¶ [l] g :=
  of_eventually_mem_of_forall_separating_mem_iff p hf hg fun U hU â†¦ (h U hU).mem_iff

theorem of_forall_separating_preimage (p : Set Î² â†’ Prop) [HasCountableSeparatingOn Î² p univ]
    (h : âˆ€ U : Set Î², p U â†’ f â»Â¹' U =á¶ [l] g â»Â¹' U) : f =á¶ [l] g :=
  of_eventually_mem_of_forall_separating_preimage p (s := univ) univ_mem univ_mem h
