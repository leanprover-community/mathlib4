/-
Copyright (c) 2022 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck, David Loeffler
-/
import Mathlib.Algebra.Module.Submodule.Basic
import Mathlib.Topology.Algebra.Monoid
import Mathlib.Analysis.Asymptotics.Asymptotics

#align_import order.filter.zero_and_bounded_at_filter from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Zero and Bounded at filter

Given a filter `l` we define the notion of a function being `ZeroAtFilter` as well as being
`BoundedAtFilter`. Alongside this we construct the `Submodule`, `AddSubmonoid` of functions
that are `ZeroAtFilter`. Similarly, we construct the `Submodule` and `Subalgebra` of functions
that are `BoundedAtFilter`.

-/


namespace Filter

variable {Œ± Œ≤ : Type*}

open Topology

/-- If `l` is a filter on `Œ±`, then a function `f : Œ± ‚Üí Œ≤` is `ZeroAtFilter l`
  if it tends to zero along `l`. -/
def ZeroAtFilter [Zero Œ≤] [TopologicalSpace Œ≤] (l : Filter Œ±) (f : Œ± ‚Üí Œ≤) : Prop :=
  Filter.Tendsto f l (ùìù 0)

theorem zero_zeroAtFilter [Zero Œ≤] [TopologicalSpace Œ≤] (l : Filter Œ±) :
    ZeroAtFilter l (0 : Œ± ‚Üí Œ≤) :=
  tendsto_const_nhds

nonrec theorem ZeroAtFilter.add [TopologicalSpace Œ≤] [AddZeroClass Œ≤] [ContinuousAdd Œ≤]
    {l : Filter Œ±} {f g : Œ± ‚Üí Œ≤} (hf : ZeroAtFilter l f) (hg : ZeroAtFilter l g) :
    ZeroAtFilter l (f + g) := by
  simpa using hf.add hg

nonrec theorem ZeroAtFilter.neg [TopologicalSpace Œ≤] [AddGroup Œ≤] [ContinuousNeg Œ≤] {l : Filter Œ±}
    {f : Œ± ‚Üí Œ≤} (hf : ZeroAtFilter l f) : ZeroAtFilter l (-f) := by simpa using hf.neg

theorem ZeroAtFilter.smul {ùïú : Type*} [TopologicalSpace ùïú] [TopologicalSpace Œ≤] [Zero ùïú] [Zero Œ≤]
    [SMulWithZero ùïú Œ≤] [ContinuousSMul ùïú Œ≤] {l : Filter Œ±} {f : Œ± ‚Üí Œ≤} (c : ùïú)
    (hf : ZeroAtFilter l f) : ZeroAtFilter l (c ‚Ä¢ f) := by simpa using hf.const_smul c

/-- `zeroAtFilterSubmodule l` is the submodule of `f : Œ± ‚Üí Œ≤` which
tend to zero along `l`. -/
def zeroAtFilterSubmodule [TopologicalSpace Œ≤] [Semiring Œ≤] [ContinuousAdd Œ≤] [ContinuousMul Œ≤]
    (l : Filter Œ±) : Submodule Œ≤ (Œ± ‚Üí Œ≤) where
  carrier := ZeroAtFilter l
  zero_mem' := zero_zeroAtFilter l
  add_mem' ha hb := ha.add hb
  smul_mem' c _ hf := hf.smul c

/-- `zeroAtFilterAddSubmonoid l` is the additive submonoid of `f : Œ± ‚Üí Œ≤`
which tend to zero along `l`. -/
def zeroAtFilterAddSubmonoid [TopologicalSpace Œ≤] [AddZeroClass Œ≤] [ContinuousAdd Œ≤]
    (l : Filter Œ±) : AddSubmonoid (Œ± ‚Üí Œ≤) where
  carrier := ZeroAtFilter l
  add_mem' ha hb := ha.add hb
  zero_mem' := zero_zeroAtFilter l

/-- If `l` is a filter on `Œ±`, then a function `f: Œ± ‚Üí Œ≤` is `BoundedAtFilter l`
if `f =O[l] 1`. -/
def BoundedAtFilter [Norm Œ≤] (l : Filter Œ±) (f : Œ± ‚Üí Œ≤) : Prop :=
  Asymptotics.IsBigO l f (1 : Œ± ‚Üí ‚Ñù)

theorem ZeroAtFilter.boundedAtFilter [NormedAddCommGroup Œ≤] {l : Filter Œ±} {f : Œ± ‚Üí Œ≤}
    (hf : ZeroAtFilter l f) : BoundedAtFilter l f := by
  rw [ZeroAtFilter, ‚Üê Asymptotics.isLittleO_const_iff (one_ne_zero' ‚Ñù)] at hf
  exact hf.isBigO

theorem const_boundedAtFilter [NormedField Œ≤] (l : Filter Œ±) (c : Œ≤) :
    BoundedAtFilter l (Function.const Œ± c : Œ± ‚Üí Œ≤) :=
  Asymptotics.isBigO_const_const c one_ne_zero l

nonrec theorem BoundedAtFilter.add [NormedAddCommGroup Œ≤] {l : Filter Œ±} {f g : Œ± ‚Üí Œ≤}
    (hf : BoundedAtFilter l f) (hg : BoundedAtFilter l g) : BoundedAtFilter l (f + g) := by
  simpa using hf.add hg

theorem BoundedAtFilter.neg [NormedAddCommGroup Œ≤] {l : Filter Œ±} {f : Œ± ‚Üí Œ≤}
    (hf : BoundedAtFilter l f) : BoundedAtFilter l (-f) :=
  hf.neg_left

theorem BoundedAtFilter.smul {ùïú : Type*} [NormedField ùïú] [NormedAddCommGroup Œ≤] [NormedSpace ùïú Œ≤]
    {l : Filter Œ±} {f : Œ± ‚Üí Œ≤} (c : ùïú) (hf : BoundedAtFilter l f) : BoundedAtFilter l (c ‚Ä¢ f) :=
  hf.const_smul_left c

nonrec theorem BoundedAtFilter.mul [NormedField Œ≤] {l : Filter Œ±} {f g : Œ± ‚Üí Œ≤}
    (hf : BoundedAtFilter l f) (hg : BoundedAtFilter l g) : BoundedAtFilter l (f * g) := by
  refine' (hf.mul hg).trans _
  convert Asymptotics.isBigO_refl (E := ‚Ñù) _ l
  simp

/-- The submodule of functions that are bounded along a filter `l`. -/
def boundedFilterSubmodule [NormedField Œ≤] (l : Filter Œ±) : Submodule Œ≤ (Œ± ‚Üí Œ≤) where
  carrier := BoundedAtFilter l
  zero_mem' := const_boundedAtFilter l 0
  add_mem' hf hg := hf.add hg
  smul_mem' c _ hf := hf.smul c

/-- The subalgebra of functions that are bounded along a filter `l`. -/
def boundedFilterSubalgebra [NormedField Œ≤] (l : Filter Œ±) : Subalgebra Œ≤ (Œ± ‚Üí Œ≤) := by
  refine' Submodule.toSubalgebra (boundedFilterSubmodule l) _ fun f g hf hg ‚Ü¶ _
  ¬∑ exact const_boundedAtFilter l (1 : Œ≤)
  ¬∑ simpa only [Pi.one_apply, mul_one, norm_mul] using hf.mul hg

end Filter
