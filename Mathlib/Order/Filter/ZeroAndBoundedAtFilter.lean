/-
Copyright (c) 2022 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck, David Loeffler
-/
import Mathlib.Algebra.Module.Submodule.Basic
import Mathlib.Topology.Algebra.Monoid
import Mathlib.Analysis.Asymptotics.Asymptotics
import Mathlib.Algebra.Algebra.Pi

#align_import order.filter.zero_and_bounded_at_filter from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Zero and Bounded at filter

Given a filter `l` we define the notion of a function being `ZeroAtFilter` as well as being
`BoundedAtFilter`. Alongside this we construct the `Submodule`, `AddSubmonoid` of functions
that are `ZeroAtFilter`. Similarly, we construct the `Submodule` and `Subalgebra` of functions
that are `BoundedAtFilter`.

-/


namespace Filter

variable {ğ•œ Î± Î² : Type*}

open Topology

/-- If `l` is a filter on `Î±`, then a function `f : Î± â†’ Î²` is `ZeroAtFilter l`
  if it tends to zero along `l`. -/
def ZeroAtFilter [Zero Î²] [TopologicalSpace Î²] (l : Filter Î±) (f : Î± â†’ Î²) : Prop :=
  Filter.Tendsto f l (ğ“ 0)
#align filter.zero_at_filter Filter.ZeroAtFilter

theorem zero_zeroAtFilter [Zero Î²] [TopologicalSpace Î²] (l : Filter Î±) :
    ZeroAtFilter l (0 : Î± â†’ Î²) :=
  tendsto_const_nhds
#align filter.zero_zero_at_filter Filter.zero_zeroAtFilter

nonrec theorem ZeroAtFilter.add [TopologicalSpace Î²] [AddZeroClass Î²] [ContinuousAdd Î²]
    {l : Filter Î±} {f g : Î± â†’ Î²} (hf : ZeroAtFilter l f) (hg : ZeroAtFilter l g) :
    ZeroAtFilter l (f + g) := by
  simpa using hf.add hg
#align filter.zero_at_filter.add Filter.ZeroAtFilter.add

nonrec theorem ZeroAtFilter.neg [TopologicalSpace Î²] [AddGroup Î²] [ContinuousNeg Î²] {l : Filter Î±}
    {f : Î± â†’ Î²} (hf : ZeroAtFilter l f) : ZeroAtFilter l (-f) := by simpa using hf.neg
#align filter.zero_at_filter.neg Filter.ZeroAtFilter.neg

theorem ZeroAtFilter.smul [TopologicalSpace Î²] [Zero ğ•œ] [Zero Î²]
    [SMulWithZero ğ•œ Î²] [ContinuousConstSMul ğ•œ Î²] {l : Filter Î±} {f : Î± â†’ Î²} (c : ğ•œ)
    (hf : ZeroAtFilter l f) : ZeroAtFilter l (c â€¢ f) := by simpa using hf.const_smul c
#align filter.zero_at_filter.smul Filter.ZeroAtFilter.smul

variable (ğ•œ) in
/-- `zeroAtFilterSubmodule l` is the submodule of `f : Î± â†’ Î²` which
tend to zero along `l`. -/
def zeroAtFilterSubmodule
    [TopologicalSpace Î²] [Semiring ğ•œ] [AddCommMonoid Î²] [Module ğ•œ Î²]
    [ContinuousAdd Î²] [ContinuousConstSMul ğ•œ Î²]
    (l : Filter Î±) : Submodule ğ•œ (Î± â†’ Î²) where
  carrier := ZeroAtFilter l
  zero_mem' := zero_zeroAtFilter l
  add_mem' ha hb := ha.add hb
  smul_mem' c _ hf := hf.smul c
#align filter.zero_at_filter_submodule Filter.zeroAtFilterSubmodule

/-- `zeroAtFilterAddSubmonoid l` is the additive submonoid of `f : Î± â†’ Î²`
which tend to zero along `l`. -/
def zeroAtFilterAddSubmonoid [TopologicalSpace Î²] [AddZeroClass Î²] [ContinuousAdd Î²]
    (l : Filter Î±) : AddSubmonoid (Î± â†’ Î²) where
  carrier := ZeroAtFilter l
  add_mem' ha hb := ha.add hb
  zero_mem' := zero_zeroAtFilter l
#align filter.zero_at_filter_add_submonoid Filter.zeroAtFilterAddSubmonoid

/-- If `l` is a filter on `Î±`, then a function `f: Î± â†’ Î²` is `BoundedAtFilter l`
if `f =O[l] 1`. -/
def BoundedAtFilter [Norm Î²] (l : Filter Î±) (f : Î± â†’ Î²) : Prop :=
  Asymptotics.IsBigO l f (1 : Î± â†’ â„)
#align filter.bounded_at_filter Filter.BoundedAtFilter

theorem ZeroAtFilter.boundedAtFilter [NormedAddCommGroup Î²] {l : Filter Î±} {f : Î± â†’ Î²}
    (hf : ZeroAtFilter l f) : BoundedAtFilter l f := by
  rw [ZeroAtFilter, â† Asymptotics.isLittleO_const_iff (one_ne_zero' â„)] at hf
  exact hf.isBigO
#align filter.zero_at_filter.bounded_at_filter Filter.ZeroAtFilter.boundedAtFilter

theorem const_boundedAtFilter [Norm Î²] (l : Filter Î±) (c : Î²) :
    BoundedAtFilter l (Function.const Î± c : Î± â†’ Î²) :=
  Asymptotics.isBigO_const_const c one_ne_zero l
#align filter.const_bounded_at_filter Filter.const_boundedAtFilter

nonrec theorem BoundedAtFilter.add [SeminormedAddCommGroup Î²] {l : Filter Î±} {f g : Î± â†’ Î²}
    (hf : BoundedAtFilter l f) (hg : BoundedAtFilter l g) : BoundedAtFilter l (f + g) := by
  simpa using hf.add hg
#align filter.bounded_at_filter.add Filter.BoundedAtFilter.add

theorem BoundedAtFilter.neg [SeminormedAddCommGroup Î²] {l : Filter Î±} {f : Î± â†’ Î²}
    (hf : BoundedAtFilter l f) : BoundedAtFilter l (-f) :=
  hf.neg_left
#align filter.bounded_at_filter.neg Filter.BoundedAtFilter.neg

theorem BoundedAtFilter.smul
    [SeminormedRing ğ•œ] [SeminormedAddCommGroup Î²] [Module ğ•œ Î²] [BoundedSMul ğ•œ Î²]
    {l : Filter Î±} {f : Î± â†’ Î²} (c : ğ•œ) (hf : BoundedAtFilter l f) : BoundedAtFilter l (c â€¢ f) :=
  hf.const_smul_left c
#align filter.bounded_at_filter.smul Filter.BoundedAtFilter.smul

nonrec theorem BoundedAtFilter.mul [SeminormedRing Î²] {l : Filter Î±} {f g : Î± â†’ Î²}
    (hf : BoundedAtFilter l f) (hg : BoundedAtFilter l g) : BoundedAtFilter l (f * g) := by
  refine (hf.mul hg).trans ?_
  convert Asymptotics.isBigO_refl (E := â„) _ l
  simp
#align filter.bounded_at_filter.mul Filter.BoundedAtFilter.mul

variable (ğ•œ) in
/-- The submodule of functions that are bounded along a filter `l`. -/
def boundedFilterSubmodule
    [SeminormedRing ğ•œ] [SeminormedAddCommGroup Î²] [Module ğ•œ Î²] [BoundedSMul ğ•œ Î²] (l : Filter Î±) :
    Submodule ğ•œ (Î± â†’ Î²) where
  carrier := BoundedAtFilter l
  zero_mem' := const_boundedAtFilter l 0
  add_mem' hf hg := hf.add hg
  smul_mem' c _ hf := hf.smul c
#align filter.bounded_filter_submodule Filter.boundedFilterSubmodule

variable (ğ•œ) in
/-- The subalgebra of functions that are bounded along a filter `l`. -/
def boundedFilterSubalgebra
    [SeminormedCommRing ğ•œ] [SeminormedRing Î²] [Algebra ğ•œ Î²] [BoundedSMul ğ•œ Î²] (l : Filter Î±) :
    Subalgebra ğ•œ (Î± â†’ Î²) :=
  Submodule.toSubalgebra
    (boundedFilterSubmodule ğ•œ l)
    (const_boundedAtFilter l (1 : Î²))
    (fun f g hf hg â†¦ by simpa only [Pi.one_apply, mul_one, norm_mul] using hf.mul hg)
#align filter.bounded_filter_subalgebra Filter.boundedFilterSubalgebra

end Filter
