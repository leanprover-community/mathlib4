/-
Copyright (c) 2023 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Order.Filter.Map

/-!
# Kernel of a filter

In this file we define the *kernel* `Filter.ker f` of a filter `f`
to be the intersection of all its sets.

We also prove that `Filter.principal` and `Filter.ker` form a Galois coinsertion
and prove other basic theorems about `Filter.ker`.
-/

open Function Set

namespace Filter

variable {Î¹ : Sort*} {Î± Î² : Type*} {f g : Filter Î±} {s : Set Î±} {a : Î±}

lemma ker_def (f : Filter Î±) : f.ker = â‹‚ s âˆˆ f, s := sInter_eq_biInter

@[simp] lemma mem_ker : a âˆˆ f.ker â†” âˆ€ s âˆˆ f, a âˆˆ s := mem_sInter
@[simp] lemma subset_ker : s âŠ† f.ker â†” âˆ€ t âˆˆ f, s âŠ† t := subset_sInter_iff

/-- `Filter.principal` forms a Galois coinsertion with `Filter.ker`. -/
def gi_principal_ker : GaloisCoinsertion (ğ“Ÿ : Set Î± â†’ Filter Î±) ker :=
  GaloisConnection.toGaloisCoinsertion (fun s f â†¦ by simp [principal_le_iff]) <| by
    simp only [le_iff_subset, subset_def, mem_ker, mem_principal]; aesop

lemma ker_mono : Monotone (ker : Filter Î± â†’ Set Î±) := gi_principal_ker.gc.monotone_u
lemma ker_surjective : Surjective (ker : Filter Î± â†’ Set Î±) := gi_principal_ker.u_surjective

@[simp] lemma ker_bot : ker (âŠ¥ : Filter Î±) = âˆ… := sInter_eq_empty_iff.2 fun _ â†¦ âŸ¨âˆ…, trivial, idâŸ©
@[simp] lemma ker_top : ker (âŠ¤ : Filter Î±) = univ := gi_principal_ker.gc.u_top
@[simp] lemma ker_eq_univ : ker f = univ â†” f = âŠ¤ := gi_principal_ker.gc.u_eq_top.trans <| by simp
@[simp] lemma ker_inf (f g : Filter Î±) : ker (f âŠ“ g) = ker f âˆ© ker g := gi_principal_ker.gc.u_inf
@[simp] lemma ker_iInf (f : Î¹ â†’ Filter Î±) : ker (â¨… i, f i) = â‹‚ i, ker (f i) :=
  gi_principal_ker.gc.u_iInf
@[simp] lemma ker_sInf (S : Set (Filter Î±)) : ker (sInf S) = â‹‚ f âˆˆ S, ker f :=
  gi_principal_ker.gc.u_sInf
@[simp] lemma ker_principal (s : Set Î±) : ker (ğ“Ÿ s) = s := gi_principal_ker.u_l_eq _

@[simp] lemma ker_pure (a : Î±) : ker (pure a) = {a} := by rw [â† principal_singleton, ker_principal]

@[simp] lemma ker_comap (m : Î± â†’ Î²) (f : Filter Î²) : ker (comap m f) = m â»Â¹' ker f := by
  ext a
  simp only [mem_ker, mem_comap, forall_exists_index, and_imp, @forall_swap (Set Î±), mem_preimage]
  exact forallâ‚‚_congr fun s _ â†¦ âŸ¨fun h â†¦ h _ Subset.rfl, fun ha t ht â†¦ ht haâŸ©

@[simp]
theorem ker_iSup (f : Î¹ â†’ Filter Î±) : ker (â¨† i, f i) = â‹ƒ i, ker (f i) := by
  refine subset_antisymm (fun x hx â†¦ ?_) ker_mono.le_map_iSup
  simp only [mem_iUnion, mem_ker] at hx âŠ¢
  contrapose! hx
  choose s hsf hxs using hx
  refine âŸ¨â‹ƒ i, s i, ?_, by simpaâŸ©
  exact mem_iSup.2 fun i â†¦ mem_of_superset (hsf i) (subset_iUnion s i)

@[simp]
theorem ker_sSup (S : Set (Filter Î±)) : ker (sSup S) = â‹ƒ f âˆˆ S, ker f := by
  simp [sSup_eq_iSup]

@[simp]
theorem ker_sup (f g : Filter Î±) : ker (f âŠ” g) = ker f âˆª ker g := by
  rw [â† sSup_pair, ker_sSup, biUnion_pair]

end Filter
