/-
Copyright (c) 2021 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov, Alex Kontorovich
-/
import Mathlib.Order.Filter.Bases

#align_import order.filter.pi from "leanprover-community/mathlib"@"ce64cd319bb6b3e82f31c2d38e79080d377be451"

/-!
# (Co)product of a family of filters

In this file we define two filters on `Î  i, Î± i` and prove some basic properties of these filters.

* `Filter.pi (f : Î  i, Filter (Î± i))` to be the maximal filter on `Î  i, Î± i` such that
  `âˆ€ i, Filter.Tendsto (Function.eval i) (Filter.pi f) (f i)`. It is defined as
  `Î  i, Filter.comap (Function.eval i) (f i)`. This is a generalization of `Filter.prod` to indexed
  products.

* `Filter.coprodáµ¢ (f : Î  i, Filter (Î± i))`: a generalization of `Filter.coprod`; it is the supremum
  of `comap (eval i) (f i)`.
-/


open Set Function

open scoped Classical
open Filter

namespace Filter

variable {Î¹ : Type*} {Î± : Î¹ â†’ Type*} {f fâ‚ fâ‚‚ : (i : Î¹) â†’ Filter (Î± i)} {s : (i : Î¹) â†’ Set (Î± i)}
  {p : âˆ€ i, Î± i â†’ Prop}

section Pi

/-- The product of an indexed family of filters. -/
def pi (f : âˆ€ i, Filter (Î± i)) : Filter (âˆ€ i, Î± i) :=
  â¨… i, comap (eval i) (f i)
#align filter.pi Filter.pi

instance pi.isCountablyGenerated [Countable Î¹] [âˆ€ i, IsCountablyGenerated (f i)] :
    IsCountablyGenerated (pi f) :=
  iInf.isCountablyGenerated _
#align filter.pi.is_countably_generated Filter.pi.isCountablyGenerated

theorem tendsto_eval_pi (f : âˆ€ i, Filter (Î± i)) (i : Î¹) : Tendsto (eval i) (pi f) (f i) :=
  tendsto_iInf' i tendsto_comap
#align filter.tendsto_eval_pi Filter.tendsto_eval_pi

theorem tendsto_pi {Î² : Type*} {m : Î² â†’ âˆ€ i, Î± i} {l : Filter Î²} :
    Tendsto m l (pi f) â†” âˆ€ i, Tendsto (fun x => m x i) l (f i) := by
  simp only [pi, tendsto_iInf, tendsto_comap_iff]; rfl
#align filter.tendsto_pi Filter.tendsto_pi

/-- If a function tends to a product `Filter.pi f` of filters, then its `i`-th component tends to
`f i`. See also `Filter.Tendsto.apply_nhds` for the special case of converging to a point in a
product of topological spaces. -/
alias âŸ¨Tendsto.apply, _âŸ© := tendsto_pi

theorem le_pi {g : Filter (âˆ€ i, Î± i)} : g â‰¤ pi f â†” âˆ€ i, Tendsto (eval i) g (f i) :=
  tendsto_pi
#align filter.le_pi Filter.le_pi

@[mono]
theorem pi_mono (h : âˆ€ i, fâ‚ i â‰¤ fâ‚‚ i) : pi fâ‚ â‰¤ pi fâ‚‚ :=
  iInf_mono fun i => comap_mono <| h i
#align filter.pi_mono Filter.pi_mono

theorem mem_pi_of_mem (i : Î¹) {s : Set (Î± i)} (hs : s âˆˆ f i) : eval i â»Â¹' s âˆˆ pi f :=
  mem_iInf_of_mem i <| preimage_mem_comap hs
#align filter.mem_pi_of_mem Filter.mem_pi_of_mem

theorem pi_mem_pi {I : Set Î¹} (hI : I.Finite) (h : âˆ€ i âˆˆ I, s i âˆˆ f i) : I.pi s âˆˆ pi f := by
  rw [pi_def, biInter_eq_iInter]
  refine mem_iInf_of_iInter hI (fun i => ?_) Subset.rfl
  exact preimage_mem_comap (h i i.2)
#align filter.pi_mem_pi Filter.pi_mem_pi

theorem mem_pi {s : Set (âˆ€ i, Î± i)} :
    s âˆˆ pi f â†” âˆƒ I : Set Î¹, I.Finite âˆ§ âˆƒ t : âˆ€ i, Set (Î± i), (âˆ€ i, t i âˆˆ f i) âˆ§ I.pi t âŠ† s := by
  constructor
  Â· simp only [pi, mem_iInf', mem_comap, pi_def]
    rintro âŸ¨I, If, V, hVf, -, rfl, -âŸ©
    choose t htf htV using hVf
    exact âŸ¨I, If, t, htf, iInterâ‚‚_mono fun i _ => htV iâŸ©
  Â· rintro âŸ¨I, If, t, htf, htsâŸ©
    exact mem_of_superset (pi_mem_pi If fun i _ => htf i) hts
#align filter.mem_pi Filter.mem_pi

theorem mem_pi' {s : Set (âˆ€ i, Î± i)} :
    s âˆˆ pi f â†” âˆƒ I : Finset Î¹, âˆƒ t : âˆ€ i, Set (Î± i), (âˆ€ i, t i âˆˆ f i) âˆ§ Set.pi (â†‘I) t âŠ† s :=
  mem_pi.trans exists_finite_iff_finset
#align filter.mem_pi' Filter.mem_pi'

theorem mem_of_pi_mem_pi [âˆ€ i, NeBot (f i)] {I : Set Î¹} (h : I.pi s âˆˆ pi f) {i : Î¹} (hi : i âˆˆ I) :
    s i âˆˆ f i := by
  rcases mem_pi.1 h with âŸ¨I', -, t, htf, htsâŸ©
  refine mem_of_superset (htf i) fun x hx => ?_
  have : âˆ€ i, (t i).Nonempty := fun i => nonempty_of_mem (htf i)
  choose g hg using this
  have : update g i x âˆˆ I'.pi t := fun j _ => by
    rcases eq_or_ne j i with (rfl | hne) <;> simp [*]
  simpa using hts this i hi
#align filter.mem_of_pi_mem_pi Filter.mem_of_pi_mem_pi

@[simp]
theorem pi_mem_pi_iff [âˆ€ i, NeBot (f i)] {I : Set Î¹} (hI : I.Finite) :
    I.pi s âˆˆ pi f â†” âˆ€ i âˆˆ I, s i âˆˆ f i :=
  âŸ¨fun h _i hi => mem_of_pi_mem_pi h hi, pi_mem_pi hIâŸ©
#align filter.pi_mem_pi_iff Filter.pi_mem_pi_iff

theorem Eventually.eval_pi {i : Î¹} (hf : âˆ€á¶  x : Î± i in f i, p i x) :
    âˆ€á¶  x : âˆ€ i : Î¹, Î± i in pi f, p i (x i) := (tendsto_eval_pi _ _).eventually hf
#align filter.eventually.eval_pi Filter.Eventually.eval_pi

theorem eventually_pi [Finite Î¹] (hf : âˆ€ i, âˆ€á¶  x in f i, p i x) :
    âˆ€á¶  x : âˆ€ i, Î± i in pi f, âˆ€ i, p i (x i) := eventually_all.2 fun _i => (hf _).eval_pi
#align filter.eventually_pi Filter.eventually_pi

theorem hasBasis_pi {Î¹' : Î¹ â†’ Type} {s : âˆ€ i, Î¹' i â†’ Set (Î± i)} {p : âˆ€ i, Î¹' i â†’ Prop}
    (h : âˆ€ i, (f i).HasBasis (p i) (s i)) :
    (pi f).HasBasis (fun If : Set Î¹ Ã— âˆ€ i, Î¹' i => If.1.Finite âˆ§ âˆ€ i âˆˆ If.1, p i (If.2 i))
      fun If : Set Î¹ Ã— âˆ€ i, Î¹' i => If.1.pi fun i => s i <| If.2 i := by
  simpa [Set.pi_def] using hasBasis_iInf' fun i => (h i).comap (eval i : (âˆ€ j, Î± j) â†’ Î± i)
#align filter.has_basis_pi Filter.hasBasis_pi

theorem le_pi_principal (s : (i : Î¹) â†’ Set (Î± i)) :
    ğ“Ÿ (univ.pi s) â‰¤ pi fun i â†¦ ğ“Ÿ (s i) :=
  le_pi.2 fun i â†¦ tendsto_principal_principal.2 fun _f hf â†¦ hf i trivial

@[simp]
theorem pi_principal [Finite Î¹] (s : (i : Î¹) â†’ Set (Î± i)) :
    pi (fun i â†¦ ğ“Ÿ (s i)) = ğ“Ÿ (univ.pi s) := by
  simp [Filter.pi, Set.pi_def]

@[simp]
theorem pi_pure [Finite Î¹] (f : (i : Î¹) â†’ Î± i) : pi (pure <| f Â·) = pure f := by
  simp only [â† principal_singleton, pi_principal, univ_pi_singleton]

@[simp]
theorem pi_inf_principal_univ_pi_eq_bot :
    pi f âŠ“ ğ“Ÿ (Set.pi univ s) = âŠ¥ â†” âˆƒ i, f i âŠ“ ğ“Ÿ (s i) = âŠ¥ := by
  constructor
  Â· simp only [inf_principal_eq_bot, mem_pi]
    contrapose!
    rintro (hsf : âˆ€ i, âˆƒá¶  x in f i, x âˆˆ s i) I - t htf hts
    have : âˆ€ i, (s i âˆ© t i).Nonempty := fun i => ((hsf i).and_eventually (htf i)).exists
    choose x hxs hxt using this
    exact hts (fun i _ => hxt i) (mem_univ_pi.2 hxs)
  Â· simp only [inf_principal_eq_bot]
    rintro âŸ¨i, hiâŸ©
    filter_upwards [mem_pi_of_mem i hi] with x using mt fun h => h i trivial
#align filter.pi_inf_principal_univ_pi_eq_bot Filter.pi_inf_principal_univ_pi_eq_bot

@[simp]
theorem pi_inf_principal_pi_eq_bot [âˆ€ i, NeBot (f i)] {I : Set Î¹} :
    pi f âŠ“ ğ“Ÿ (Set.pi I s) = âŠ¥ â†” âˆƒ i âˆˆ I, f i âŠ“ ğ“Ÿ (s i) = âŠ¥ := by
  rw [â† univ_pi_piecewise_univ I, pi_inf_principal_univ_pi_eq_bot]
  refine exists_congr fun i => ?_
  by_cases hi : i âˆˆ I <;> simp [hi, NeBot.ne']
#align filter.pi_inf_principal_pi_eq_bot Filter.pi_inf_principal_pi_eq_bot

@[simp]
theorem pi_inf_principal_univ_pi_neBot :
    NeBot (pi f âŠ“ ğ“Ÿ (Set.pi univ s)) â†” âˆ€ i, NeBot (f i âŠ“ ğ“Ÿ (s i)) := by simp [neBot_iff]
#align filter.pi_inf_principal_univ_pi_ne_bot Filter.pi_inf_principal_univ_pi_neBot

@[simp]
theorem pi_inf_principal_pi_neBot [âˆ€ i, NeBot (f i)] {I : Set Î¹} :
    NeBot (pi f âŠ“ ğ“Ÿ (I.pi s)) â†” âˆ€ i âˆˆ I, NeBot (f i âŠ“ ğ“Ÿ (s i)) := by simp [neBot_iff]
#align filter.pi_inf_principal_pi_ne_bot Filter.pi_inf_principal_pi_neBot

instance PiInfPrincipalPi.neBot [h : âˆ€ i, NeBot (f i âŠ“ ğ“Ÿ (s i))] {I : Set Î¹} :
    NeBot (pi f âŠ“ ğ“Ÿ (I.pi s)) :=
  (pi_inf_principal_univ_pi_neBot.2 â€¹_â€º).mono <|
    inf_le_inf_left _ <| principal_mono.2 fun x hx i _ => hx i trivial
#align filter.pi_inf_principal_pi.ne_bot Filter.PiInfPrincipalPi.neBot

@[simp]
theorem pi_eq_bot : pi f = âŠ¥ â†” âˆƒ i, f i = âŠ¥ := by
  simpa using @pi_inf_principal_univ_pi_eq_bot Î¹ Î± f fun _ => univ
#align filter.pi_eq_bot Filter.pi_eq_bot

@[simp]
theorem pi_neBot : NeBot (pi f) â†” âˆ€ i, NeBot (f i) := by simp [neBot_iff]
#align filter.pi_ne_bot Filter.pi_neBot

instance [âˆ€ i, NeBot (f i)] : NeBot (pi f) :=
  pi_neBot.2 â€¹_â€º

@[simp]
theorem map_eval_pi (f : âˆ€ i, Filter (Î± i)) [âˆ€ i, NeBot (f i)] (i : Î¹) :
    map (eval i) (pi f) = f i := by
  refine le_antisymm (tendsto_eval_pi f i) fun s hs => ?_
  rcases mem_pi.1 (mem_map.1 hs) with âŸ¨I, hIf, t, htf, hIâŸ©
  rw [â† image_subset_iff] at hI
  refine mem_of_superset (htf i) ((subset_eval_image_pi ?_ _).trans hI)
  exact nonempty_of_mem (pi_mem_pi hIf fun i _ => htf i)
#align filter.map_eval_pi Filter.map_eval_pi

@[simp]
theorem pi_le_pi [âˆ€ i, NeBot (fâ‚ i)] : pi fâ‚ â‰¤ pi fâ‚‚ â†” âˆ€ i, fâ‚ i â‰¤ fâ‚‚ i :=
  âŸ¨fun h i => map_eval_pi fâ‚ i â–¸ (tendsto_eval_pi _ _).mono_left h, pi_monoâŸ©
#align filter.pi_le_pi Filter.pi_le_pi

@[simp]
theorem pi_inj [âˆ€ i, NeBot (fâ‚ i)] : pi fâ‚ = pi fâ‚‚ â†” fâ‚ = fâ‚‚ := by
  refine âŸ¨fun h => ?_, congr_arg piâŸ©
  have hle : fâ‚ â‰¤ fâ‚‚ := pi_le_pi.1 h.le
  haveI : âˆ€ i, NeBot (fâ‚‚ i) := fun i => neBot_of_le (hle i)
  exact hle.antisymm (pi_le_pi.1 h.ge)
#align filter.pi_inj Filter.pi_inj

end Pi

/-! ### `n`-ary coproducts of filters -/

section CoprodCat

-- for "Coprod"
set_option linter.uppercaseLean3 false

/-- Coproduct of filters. -/
protected def coprodáµ¢ (f : âˆ€ i, Filter (Î± i)) : Filter (âˆ€ i, Î± i) :=
  â¨† i : Î¹, comap (eval i) (f i)
#align filter.Coprod Filter.coprodáµ¢

theorem mem_coprodáµ¢_iff {s : Set (âˆ€ i, Î± i)} :
    s âˆˆ Filter.coprodáµ¢ f â†” âˆ€ i : Î¹, âˆƒ tâ‚ âˆˆ f i, eval i â»Â¹' tâ‚ âŠ† s := by simp [Filter.coprodáµ¢]
#align filter.mem_Coprod_iff Filter.mem_coprodáµ¢_iff

theorem compl_mem_coprodáµ¢ {s : Set (âˆ€ i, Î± i)} :
    sá¶œ âˆˆ Filter.coprodáµ¢ f â†” âˆ€ i, (eval i '' s)á¶œ âˆˆ f i := by
  simp only [Filter.coprodáµ¢, mem_iSup, compl_mem_comap]
#align filter.compl_mem_Coprod Filter.compl_mem_coprodáµ¢

theorem coprodáµ¢_neBot_iff' :
    NeBot (Filter.coprodáµ¢ f) â†” (âˆ€ i, Nonempty (Î± i)) âˆ§ âˆƒ d, NeBot (f d) := by
  simp only [Filter.coprodáµ¢, iSup_neBot, â† exists_and_left, â† comap_eval_neBot_iff']
#align filter.Coprod_ne_bot_iff' Filter.coprodáµ¢_neBot_iff'

@[simp]
theorem coprodáµ¢_neBot_iff [âˆ€ i, Nonempty (Î± i)] : NeBot (Filter.coprodáµ¢ f) â†” âˆƒ d, NeBot (f d) := by
  simp [coprodáµ¢_neBot_iff', *]
#align filter.Coprod_ne_bot_iff Filter.coprodáµ¢_neBot_iff

theorem coprodáµ¢_eq_bot_iff' : Filter.coprodáµ¢ f = âŠ¥ â†” (âˆƒ i, IsEmpty (Î± i)) âˆ¨ f = âŠ¥ := by
  simpa only [not_neBot, not_and_or, funext_iff, not_forall, not_exists, not_nonempty_iff]
    using coprodáµ¢_neBot_iff'.not
#align filter.Coprod_eq_bot_iff' Filter.coprodáµ¢_eq_bot_iff'

@[simp]
theorem coprodáµ¢_eq_bot_iff [âˆ€ i, Nonempty (Î± i)] : Filter.coprodáµ¢ f = âŠ¥ â†” f = âŠ¥ := by
  simpa [funext_iff] using coprodáµ¢_neBot_iff.not
#align filter.Coprod_eq_bot_iff Filter.coprodáµ¢_eq_bot_iff

@[simp] theorem coprodáµ¢_bot' : Filter.coprodáµ¢ (âŠ¥ : âˆ€ i, Filter (Î± i)) = âŠ¥ :=
  coprodáµ¢_eq_bot_iff'.2 (Or.inr rfl)
#align filter.Coprod_bot' Filter.coprodáµ¢_bot'

@[simp]
theorem coprodáµ¢_bot : Filter.coprodáµ¢ (fun _ => âŠ¥ : âˆ€ i, Filter (Î± i)) = âŠ¥ :=
  coprodáµ¢_bot'
#align filter.Coprod_bot Filter.coprodáµ¢_bot

theorem NeBot.coprodáµ¢ [âˆ€ i, Nonempty (Î± i)] {i : Î¹} (h : NeBot (f i)) : NeBot (Filter.coprodáµ¢ f) :=
  coprodáµ¢_neBot_iff.2 âŸ¨i, hâŸ©
#align filter.ne_bot.Coprod Filter.NeBot.coprodáµ¢

@[instance]
theorem coprodáµ¢_neBot [âˆ€ i, Nonempty (Î± i)] [Nonempty Î¹] (f : âˆ€ i, Filter (Î± i))
    [H : âˆ€ i, NeBot (f i)] : NeBot (Filter.coprodáµ¢ f) :=
  (H (Classical.arbitrary Î¹)).coprodáµ¢
#align filter.Coprod_ne_bot Filter.coprodáµ¢_neBot

@[mono]
theorem coprodáµ¢_mono (hf : âˆ€ i, fâ‚ i â‰¤ fâ‚‚ i) : Filter.coprodáµ¢ fâ‚ â‰¤ Filter.coprodáµ¢ fâ‚‚ :=
  iSup_mono fun i => comap_mono (hf i)
#align filter.Coprod_mono Filter.coprodáµ¢_mono

variable {Î² : Î¹ â†’ Type*} {m : âˆ€ i, Î± i â†’ Î² i}

theorem map_pi_map_coprodáµ¢_le :
    map (fun k : âˆ€ i, Î± i => fun i => m i (k i)) (Filter.coprodáµ¢ f) â‰¤
      Filter.coprodáµ¢ fun i => map (m i) (f i) := by
  simp only [le_def, mem_map, mem_coprodáµ¢_iff]
  intro s h i
  obtain âŸ¨t, H, hHâŸ© := h i
  exact âŸ¨{ x : Î± i | m i x âˆˆ t }, H, fun x hx => hH hxâŸ©
#align filter.map_pi_map_Coprod_le Filter.map_pi_map_coprodáµ¢_le

theorem Tendsto.pi_map_coprodáµ¢ {g : âˆ€ i, Filter (Î² i)} (h : âˆ€ i, Tendsto (m i) (f i) (g i)) :
    Tendsto (fun k : âˆ€ i, Î± i => fun i => m i (k i)) (Filter.coprodáµ¢ f) (Filter.coprodáµ¢ g) :=
  map_pi_map_coprodáµ¢_le.trans (coprodáµ¢_mono h)
#align filter.tendsto.pi_map_Coprod Filter.Tendsto.pi_map_coprodáµ¢

end CoprodCat

end Filter
