/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Alex Kontorovich
-/
import Mathlib.Data.Set.Piecewise
import Mathlib.Order.Filter.Tendsto
import Mathlib.Order.Filter.Bases.Finite

/-!
# (Co)product of a family of filters

In this file we define two filters on `Î  i, Î± i` and prove some basic properties of these filters.

* `Filter.pi (f : Î  i, Filter (Î± i))` to be the maximal filter on `Î  i, Î± i` such that
  `âˆ€ i, Filter.Tendsto (Function.eval i) (Filter.pi f) (f i)`. It is defined as
  `Î  i, Filter.comap (Function.eval i) (f i)`. This is a generalization of `Filter.prod` to indexed
  products.

* `Filter.coprodáµ¢ (f : Î  i, Filter (Î± i))`: a generalization of `Filter.coprod`; it is the supremum
  of `comap (eval i) (f i)`.
-/


open Set Function Filter

namespace Filter

variable {Î¹ : Type*} {Î± : Î¹ â†’ Type*} {f fâ‚ fâ‚‚ : (i : Î¹) â†’ Filter (Î± i)} {s : (i : Î¹) â†’ Set (Î± i)}
  {p : âˆ€ i, Î± i â†’ Prop}

section Pi

theorem tendsto_eval_pi (f : âˆ€ i, Filter (Î± i)) (i : Î¹) : Tendsto (eval i) (pi f) (f i) :=
  tendsto_iInf' i tendsto_comap

theorem tendsto_pi {Î² : Type*} {m : Î² â†’ âˆ€ i, Î± i} {l : Filter Î²} :
    Tendsto m l (pi f) â†” âˆ€ i, Tendsto (fun x => m x i) l (f i) := by
  simp only [pi, tendsto_iInf, tendsto_comap_iff]; rfl

/-- If a function tends to a product `Filter.pi f` of filters, then its `i`-th component tends to
`f i`. See also `Filter.Tendsto.apply_nhds` for the special case of converging to a point in a
product of topological spaces. -/
alias âŸ¨Tendsto.apply, _âŸ© := tendsto_pi

theorem le_pi {g : Filter (âˆ€ i, Î± i)} : g â‰¤ pi f â†” âˆ€ i, Tendsto (eval i) g (f i) :=
  tendsto_pi

@[mono]
theorem pi_mono (h : âˆ€ i, fâ‚ i â‰¤ fâ‚‚ i) : pi fâ‚ â‰¤ pi fâ‚‚ :=
  iInf_mono fun i => comap_mono <| h i

theorem mem_pi_of_mem (i : Î¹) {s : Set (Î± i)} (hs : s âˆˆ f i) : eval i â»Â¹' s âˆˆ pi f :=
  mem_iInf_of_mem i <| preimage_mem_comap hs

theorem pi_mem_pi {I : Set Î¹} (hI : I.Finite) (h : âˆ€ i âˆˆ I, s i âˆˆ f i) : I.pi s âˆˆ pi f := by
  rw [pi_def, biInter_eq_iInter]
  refine mem_iInf_of_iInter hI (fun i => ?_) Subset.rfl
  exact preimage_mem_comap (h i i.2)

theorem mem_pi {s : Set (âˆ€ i, Î± i)} :
    s âˆˆ pi f â†” âˆƒ I : Set Î¹, I.Finite âˆ§ âˆƒ t : âˆ€ i, Set (Î± i), (âˆ€ i, t i âˆˆ f i) âˆ§ I.pi t âŠ† s := by
  constructor
  Â· simp only [pi, mem_iInf', mem_comap, pi_def]
    rintro âŸ¨I, If, V, hVf, -, rfl, -âŸ©
    choose t htf htV using hVf
    exact âŸ¨I, If, t, htf, iInterâ‚‚_mono fun i _ => htV iâŸ©
  Â· rintro âŸ¨I, If, t, htf, htsâŸ©
    exact mem_of_superset (pi_mem_pi If fun i _ => htf i) hts

theorem mem_pi' {s : Set (âˆ€ i, Î± i)} :
    s âˆˆ pi f â†” âˆƒ I : Finset Î¹, âˆƒ t : âˆ€ i, Set (Î± i), (âˆ€ i, t i âˆˆ f i) âˆ§ Set.pi (â†‘I) t âŠ† s :=
  mem_pi.trans exists_finite_iff_finset

theorem mem_of_pi_mem_pi [âˆ€ i, NeBot (f i)] {I : Set Î¹} (h : I.pi s âˆˆ pi f) {i : Î¹} (hi : i âˆˆ I) :
    s i âˆˆ f i := by
  classical
  rcases mem_pi.1 h with âŸ¨I', -, t, htf, htsâŸ©
  refine mem_of_superset (htf i) fun x hx => ?_
  have : âˆ€ i, (t i).Nonempty := fun i => nonempty_of_mem (htf i)
  choose g hg using this
  have : update g i x âˆˆ I'.pi t := fun j _ => by
    rcases eq_or_ne j i with (rfl | hne) <;> simp [*]
  simpa using hts this i hi

@[simp]
theorem pi_mem_pi_iff [âˆ€ i, NeBot (f i)] {I : Set Î¹} (hI : I.Finite) :
    I.pi s âˆˆ pi f â†” âˆ€ i âˆˆ I, s i âˆˆ f i :=
  âŸ¨fun h _i hi => mem_of_pi_mem_pi h hi, pi_mem_pi hIâŸ©

theorem Eventually.eval_pi {i : Î¹} (hf : âˆ€á¶  x : Î± i in f i, p i x) :
    âˆ€á¶  x : âˆ€ i : Î¹, Î± i in pi f, p i (x i) := (tendsto_eval_pi _ _).eventually hf

theorem eventually_pi [Finite Î¹] (hf : âˆ€ i, âˆ€á¶  x in f i, p i x) :
    âˆ€á¶  x : âˆ€ i, Î± i in pi f, âˆ€ i, p i (x i) := eventually_all.2 fun _i => (hf _).eval_pi

theorem hasBasis_pi {Î¹' : Î¹ â†’ Type*} {s : âˆ€ i, Î¹' i â†’ Set (Î± i)} {p : âˆ€ i, Î¹' i â†’ Prop}
    (h : âˆ€ i, (f i).HasBasis (p i) (s i)) :
    (pi f).HasBasis (fun If : Set Î¹ Ã— âˆ€ i, Î¹' i => If.1.Finite âˆ§ âˆ€ i âˆˆ If.1, p i (If.2 i))
      fun If : Set Î¹ Ã— âˆ€ i, Î¹' i => If.1.pi fun i => s i <| If.2 i := by
  simpa [Set.pi_def] using HasBasis.iInf' fun i => (h i).comap (eval i : (âˆ€ j, Î± j) â†’ Î± i)

theorem hasBasis_pi_same_index {Îº : Type*} {p : Îº â†’ Prop} {s : Î  i : Î¹, Îº â†’ Set (Î± i)}
    (h : âˆ€ i : Î¹, (f i).HasBasis p (s i))
    (h_dir : âˆ€ I : Set Î¹, âˆ€ k : Î¹ â†’ Îº, I.Finite â†’ (âˆ€ i âˆˆ I, p (k i)) â†’
      âˆƒ kâ‚€, p kâ‚€ âˆ§ âˆ€ i âˆˆ I, s i kâ‚€ âŠ† s i (k i)) :
    (pi f).HasBasis (fun Ik : Set Î¹ Ã— Îº â†¦ Ik.1.Finite âˆ§ p Ik.2)
      (fun Ik â†¦ Ik.1.pi (fun i â†¦ s i Ik.2)) := by
  refine hasBasis_pi h |>.to_hasBasis ?_ ?_
  Â· rintro âŸ¨I, kâŸ© âŸ¨hI, hkâŸ©
    rcases h_dir I k hI hk with âŸ¨kâ‚€, hkâ‚€, hkâ‚€'âŸ©
    exact âŸ¨âŸ¨I, kâ‚€âŸ©, âŸ¨hI, hkâ‚€âŸ©, Set.pi_mono hkâ‚€'âŸ©
  Â· rintro âŸ¨I, kâŸ© âŸ¨hI, hkâŸ©
    exact âŸ¨âŸ¨I, fun _ â†¦ kâŸ©, âŸ¨hI, fun _ _ â†¦ hkâŸ©, subset_rflâŸ©

theorem HasBasis.pi_self {Î± : Type*} {Îº : Type*} {f : Filter Î±} {p : Îº â†’ Prop} {s : Îº â†’ Set Î±}
    (h : f.HasBasis p s) :
    (pi fun _ â†¦ f).HasBasis (fun Ik : Set Î¹ Ã— Îº â†¦ Ik.1.Finite âˆ§ p Ik.2)
      (fun Ik â†¦ Ik.1.pi (fun _ â†¦ s Ik.2)) := by
  refine hasBasis_pi_same_index (fun _ â†¦ h) (fun I k hI hk â†¦ ?_)
  rcases h.mem_iff.mp (biInter_mem hI |>.mpr fun i hi â†¦ h.mem_of_mem (hk i hi))
    with âŸ¨kâ‚€, hkâ‚€, hkâ‚€'âŸ©
  exact âŸ¨kâ‚€, hkâ‚€, fun i hi â†¦ hkâ‚€'.trans (biInter_subset_of_mem hi)âŸ©

theorem le_pi_principal (s : (i : Î¹) â†’ Set (Î± i)) :
    ğ“Ÿ (univ.pi s) â‰¤ pi fun i â†¦ ğ“Ÿ (s i) :=
  le_pi.2 fun i â†¦ tendsto_principal_principal.2 fun _f hf â†¦ hf i trivial

/-- The indexed product of finitely many principal filters
is the principal filter corresponding to the cylinder `Set.univ.pi s`.

If the index type is infinite, then `mem_pi_principal` and `hasBasis_pi_principal` may be useful. -/
@[simp]
theorem pi_principal [Finite Î¹] (s : (i : Î¹) â†’ Set (Î± i)) :
    pi (fun i â†¦ ğ“Ÿ (s i)) = ğ“Ÿ (univ.pi s) := by
  simp [Filter.pi, Set.pi_def]

/-- The indexed product of a (possibly, infinite) family of principal filters
is generated by the finite `Set.pi` cylinders.

If the index type is finite, then the indexed product of principal filters
is a principal filter, see `pi_principal`. -/
theorem mem_pi_principal {t : Set ((i : Î¹) â†’ Î± i)} :
    t âˆˆ pi (fun i â†¦ ğ“Ÿ (s i)) â†” âˆƒ I : Set Î¹, I.Finite âˆ§ I.pi s âŠ† t :=
  (hasBasis_pi (fun i â†¦ hasBasis_principal _)).mem_iff.trans <| by simp

/-- The indexed product of a (possibly, infinite) family of principal filters
is generated by the finite `Set.pi` cylinders.

If the index type is finite, then the indexed product of principal filters
is a principal filter, see `pi_principal`. -/
theorem hasBasis_pi_principal (s : (i : Î¹) â†’ Set (Î± i)) :
    HasBasis (pi fun i â†¦ ğ“Ÿ (s i)) Set.Finite (Set.pi Â· s) :=
  âŸ¨fun _ â†¦ mem_pi_principalâŸ©

/-- The indexed product of finitely many pure filters `pure (f i)` is the pure filter `pure f`.

If the index type is infinite, then `mem_pi_pure` and `hasBasis_pi_pure` below may be useful. -/
@[simp]
theorem pi_pure [Finite Î¹] (f : (i : Î¹) â†’ Î± i) : pi (pure <| f Â·) = pure f := by
  simp only [â† principal_singleton, pi_principal, univ_pi_singleton]

/-- The indexed product of a (possibly, infinite) family of pure filters `pure (f i)`
is generated by the sets of functions that are equal to `f` on a finite set.

If the index type is finite, then the indexed product of pure filters is a pure filter,
see `pi_pure`. -/
theorem mem_pi_pure {f : (i : Î¹) â†’ Î± i} {s : Set ((i : Î¹) â†’ Î± i)} :
    s âˆˆ pi (fun i â†¦ pure (f i)) â†” âˆƒ I : Set Î¹, I.Finite âˆ§ âˆ€ g, (âˆ€ i âˆˆ I, g i = f i) â†’ g âˆˆ s := by
  simp only [â† principal_singleton, mem_pi_principal]
  simp [subset_def]

/-- The indexed product of a (possibly, infinite) family of pure filters `pure (f i)`
is generated by the sets of functions that are equal to `f` on a finite set.

If the index type is finite, then the indexed product of pure filters is a pure filter,
see `pi_pure`. -/
theorem hasBasis_pi_pure (f : (i : Î¹) â†’ Î± i) :
    HasBasis (pi fun i â†¦ pure (f i)) Set.Finite (fun I â†¦ {g | âˆ€ i âˆˆ I, g i = f i}) :=
  âŸ¨fun _ â†¦ mem_pi_pureâŸ©

@[simp]
theorem pi_inf_principal_univ_pi_eq_bot :
    pi f âŠ“ ğ“Ÿ (Set.pi univ s) = âŠ¥ â†” âˆƒ i, f i âŠ“ ğ“Ÿ (s i) = âŠ¥ := by
  constructor
  Â· simp only [inf_principal_eq_bot, mem_pi]
    contrapose!
    rintro (hsf : âˆ€ i, âˆƒá¶  x in f i, x âˆˆ s i) I - t htf hts
    have : âˆ€ i, (s i âˆ© t i).Nonempty := fun i => ((hsf i).and_eventually (htf i)).exists
    choose x hxs hxt using this
    exact hts (fun i _ => hxt i) (mem_univ_pi.2 hxs)
  Â· simp only [inf_principal_eq_bot]
    rintro âŸ¨i, hiâŸ©
    filter_upwards [mem_pi_of_mem i hi] with x using mt fun h => h i trivial

@[simp]
theorem pi_inf_principal_pi_eq_bot [âˆ€ i, NeBot (f i)] {I : Set Î¹} :
    pi f âŠ“ ğ“Ÿ (Set.pi I s) = âŠ¥ â†” âˆƒ i âˆˆ I, f i âŠ“ ğ“Ÿ (s i) = âŠ¥ := by
  classical
  rw [â† univ_pi_piecewise_univ I, pi_inf_principal_univ_pi_eq_bot]
  refine exists_congr fun i => ?_
  by_cases hi : i âˆˆ I <;> simp [hi, NeBot.ne']

@[simp]
theorem pi_inf_principal_univ_pi_neBot :
    NeBot (pi f âŠ“ ğ“Ÿ (Set.pi univ s)) â†” âˆ€ i, NeBot (f i âŠ“ ğ“Ÿ (s i)) := by simp [neBot_iff]

@[simp]
theorem pi_inf_principal_pi_neBot [âˆ€ i, NeBot (f i)] {I : Set Î¹} :
    NeBot (pi f âŠ“ ğ“Ÿ (I.pi s)) â†” âˆ€ i âˆˆ I, NeBot (f i âŠ“ ğ“Ÿ (s i)) := by simp [neBot_iff]

instance PiInfPrincipalPi.neBot [h : âˆ€ i, NeBot (f i âŠ“ ğ“Ÿ (s i))] {I : Set Î¹} :
    NeBot (pi f âŠ“ ğ“Ÿ (I.pi s)) :=
  (pi_inf_principal_univ_pi_neBot.2 â€¹_â€º).mono <|
    inf_le_inf_left _ <| principal_mono.2 fun _ hx i _ => hx i trivial

@[simp]
theorem pi_eq_bot : pi f = âŠ¥ â†” âˆƒ i, f i = âŠ¥ := by
  simpa using @pi_inf_principal_univ_pi_eq_bot Î¹ Î± f fun _ => univ

@[simp]
theorem pi_neBot : NeBot (pi f) â†” âˆ€ i, NeBot (f i) := by simp [neBot_iff]

instance [âˆ€ i, NeBot (f i)] : NeBot (pi f) :=
  pi_neBot.2 â€¹_â€º

@[simp]
theorem map_eval_pi (f : âˆ€ i, Filter (Î± i)) [âˆ€ i, NeBot (f i)] (i : Î¹) :
    map (eval i) (pi f) = f i := by
  refine le_antisymm (tendsto_eval_pi f i) fun s hs => ?_
  rcases mem_pi.1 (mem_map.1 hs) with âŸ¨I, hIf, t, htf, hIâŸ©
  rw [â† image_subset_iff] at hI
  refine mem_of_superset (htf i) ((subset_eval_image_pi ?_ _).trans hI)
  exact nonempty_of_mem (pi_mem_pi hIf fun i _ => htf i)

@[simp]
theorem pi_le_pi [âˆ€ i, NeBot (fâ‚ i)] : pi fâ‚ â‰¤ pi fâ‚‚ â†” âˆ€ i, fâ‚ i â‰¤ fâ‚‚ i :=
  âŸ¨fun h i => map_eval_pi fâ‚ i â–¸ (tendsto_eval_pi _ _).mono_left h, pi_monoâŸ©

@[simp]
theorem pi_inj [âˆ€ i, NeBot (fâ‚ i)] : pi fâ‚ = pi fâ‚‚ â†” fâ‚ = fâ‚‚ := by
  refine âŸ¨fun h => ?_, congr_arg piâŸ©
  have hle : fâ‚ â‰¤ fâ‚‚ := pi_le_pi.1 h.le
  haveI : âˆ€ i, NeBot (fâ‚‚ i) := fun i => neBot_of_le (hle i)
  exact hle.antisymm (pi_le_pi.1 h.ge)

theorem tendsto_piMap_pi {Î² : Î¹ â†’ Type*} {f : âˆ€ i, Î± i â†’ Î² i} {l : âˆ€ i, Filter (Î± i)}
    {l' : âˆ€ i, Filter (Î² i)} (h : âˆ€ i, Tendsto (f i) (l i) (l' i)) :
    Tendsto (Pi.map f) (pi l) (pi l') :=
  tendsto_pi.2 fun i â†¦ (h i).comp (tendsto_eval_pi _ _)

theorem pi_comap {Î² : Î¹ â†’ Type*} {f : âˆ€ i, Î± i â†’ Î² i} {l : âˆ€ i, Filter (Î² i)} :
    pi (fun i â†¦ comap (f i) (l i)) = comap (Pi.map f) (pi l) := by
  simp [Filter.pi, Filter.comap_comap, Function.comp_def]

end Pi

/-! ### `n`-ary coproducts of filters -/

section CoprodCat

-- for "Coprod"

/-- Coproduct of filters. -/
protected def coprodáµ¢ (f : âˆ€ i, Filter (Î± i)) : Filter (âˆ€ i, Î± i) :=
  â¨† i : Î¹, comap (eval i) (f i)

theorem mem_coprodáµ¢_iff {s : Set (âˆ€ i, Î± i)} :
    s âˆˆ Filter.coprodáµ¢ f â†” âˆ€ i : Î¹, âˆƒ tâ‚ âˆˆ f i, eval i â»Â¹' tâ‚ âŠ† s := by simp [Filter.coprodáµ¢]

theorem compl_mem_coprodáµ¢ {s : Set (âˆ€ i, Î± i)} :
    sá¶œ âˆˆ Filter.coprodáµ¢ f â†” âˆ€ i, (eval i '' s)á¶œ âˆˆ f i := by
  simp only [Filter.coprodáµ¢, mem_iSup, compl_mem_comap]

theorem coprodáµ¢_neBot_iff' :
    NeBot (Filter.coprodáµ¢ f) â†” (âˆ€ i, Nonempty (Î± i)) âˆ§ âˆƒ d, NeBot (f d) := by
  simp only [Filter.coprodáµ¢, iSup_neBot, â† exists_and_left, â† comap_eval_neBot_iff']

@[simp]
theorem coprodáµ¢_neBot_iff [âˆ€ i, Nonempty (Î± i)] : NeBot (Filter.coprodáµ¢ f) â†” âˆƒ d, NeBot (f d) := by
  simp [coprodáµ¢_neBot_iff', *]

theorem coprodáµ¢_eq_bot_iff' : Filter.coprodáµ¢ f = âŠ¥ â†” (âˆƒ i, IsEmpty (Î± i)) âˆ¨ f = âŠ¥ := by
  simpa only [not_neBot, not_and_or, funext_iff, not_forall, not_exists, not_nonempty_iff]
    using coprodáµ¢_neBot_iff'.not

@[simp]
theorem coprodáµ¢_eq_bot_iff [âˆ€ i, Nonempty (Î± i)] : Filter.coprodáµ¢ f = âŠ¥ â†” f = âŠ¥ := by
  simpa [funext_iff] using coprodáµ¢_neBot_iff.not

@[simp] theorem coprodáµ¢_bot' : Filter.coprodáµ¢ (âŠ¥ : âˆ€ i, Filter (Î± i)) = âŠ¥ :=
  coprodáµ¢_eq_bot_iff'.2 (Or.inr rfl)

@[simp]
theorem coprodáµ¢_bot : Filter.coprodáµ¢ (fun _ => âŠ¥ : âˆ€ i, Filter (Î± i)) = âŠ¥ :=
  coprodáµ¢_bot'

theorem NeBot.coprodáµ¢ [âˆ€ i, Nonempty (Î± i)] {i : Î¹} (h : NeBot (f i)) : NeBot (Filter.coprodáµ¢ f) :=
  coprodáµ¢_neBot_iff.2 âŸ¨i, hâŸ©

@[instance]
theorem coprodáµ¢_neBot [âˆ€ i, Nonempty (Î± i)] [Nonempty Î¹] (f : âˆ€ i, Filter (Î± i))
    [H : âˆ€ i, NeBot (f i)] : NeBot (Filter.coprodáµ¢ f) :=
  (H (Classical.arbitrary Î¹)).coprodáµ¢

@[mono]
theorem coprodáµ¢_mono (hf : âˆ€ i, fâ‚ i â‰¤ fâ‚‚ i) : Filter.coprodáµ¢ fâ‚ â‰¤ Filter.coprodáµ¢ fâ‚‚ :=
  iSup_mono fun i => comap_mono (hf i)

variable {Î² : Î¹ â†’ Type*} {m : âˆ€ i, Î± i â†’ Î² i}

theorem map_pi_map_coprodáµ¢_le :
    map (fun k : âˆ€ i, Î± i => fun i => m i (k i)) (Filter.coprodáµ¢ f) â‰¤
      Filter.coprodáµ¢ fun i => map (m i) (f i) := by
  simp only [le_def, mem_map, mem_coprodáµ¢_iff]
  intro s h i
  obtain âŸ¨t, H, hHâŸ© := h i
  exact âŸ¨{ x : Î± i | m i x âˆˆ t }, H, fun x hx => hH hxâŸ©

theorem Tendsto.pi_map_coprodáµ¢ {g : âˆ€ i, Filter (Î² i)} (h : âˆ€ i, Tendsto (m i) (f i) (g i)) :
    Tendsto (fun k : âˆ€ i, Î± i => fun i => m i (k i)) (Filter.coprodáµ¢ f) (Filter.coprodáµ¢ g) :=
  map_pi_map_coprodáµ¢_le.trans (coprodáµ¢_mono h)

end CoprodCat

end Filter
