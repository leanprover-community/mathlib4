/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Jeremy Avigad, Yury Kudryashov
-/
import Mathlib.Order.Filter.Map
import Mathlib.Order.ZornAtoms

/-!
# Ultrafilters

An ultrafilter is a minimal (maximal in the set order) proper filter.
In this file we define

* `Ultrafilter.of`: an ultrafilter that is less than or equal to a given filter;
* `Ultrafilter`: subtype of ultrafilters;
* `pure x : Ultrafilter Î±`: `pure x` as an `Ultrafilter`;
* `Ultrafilter.map`, `Ultrafilter.bind`, `Ultrafilter.comap` : operations on ultrafilters;
-/

assert_not_exists Set.Finite

universe u v

variable {Î± : Type u} {Î² : Type v} {Î³ : Type*}

open Set Filter Function

/-- `Filter Î±` is an atomic type: for every filter there exists an ultrafilter that is less than or
equal to this filter. -/
instance : IsAtomic (Filter Î±) :=
  IsAtomic.of_isChain_bounded fun c hc hne hb =>
    âŸ¨sInf c, (sInf_neBot_of_directed' hne (show IsChain (Â· â‰¥ Â·) c from hc.symm).directedOn hb).ne,
      fun _ hx => sInf_le hxâŸ©

/-- An ultrafilter is a minimal (maximal in the set order) proper filter. -/
structure Ultrafilter (Î± : Type*) extends Filter Î± where
  /-- An ultrafilter is nontrivial. -/
  protected neBot' : NeBot toFilter
  /-- If `g` is a nontrivial filter that is less than or equal to an ultrafilter, then it is greater
  than or equal to the ultrafilter. -/
  protected le_of_le : âˆ€ g, Filter.NeBot g â†’ g â‰¤ toFilter â†’ toFilter â‰¤ g

namespace Ultrafilter

variable {f g : Ultrafilter Î±} {s t : Set Î±} {p q : Î± â†’ Prop}

attribute [coe] Ultrafilter.toFilter

instance : CoeTC (Ultrafilter Î±) (Filter Î±) :=
  âŸ¨Ultrafilter.toFilterâŸ©

instance : Membership (Set Î±) (Ultrafilter Î±) :=
  âŸ¨fun f s => s âˆˆ (f : Filter Î±)âŸ©

theorem unique (f : Ultrafilter Î±) {g : Filter Î±} (h : g â‰¤ f) (hne : NeBot g := by infer_instance) :
    g = f :=
  le_antisymm h <| f.le_of_le g hne h

instance neBot (f : Ultrafilter Î±) : NeBot (f : Filter Î±) :=
  f.neBot'

protected theorem isAtom (f : Ultrafilter Î±) : IsAtom (f : Filter Î±) :=
  âŸ¨f.neBot.ne, fun _ hgf => by_contra fun hg => hgf.ne <| f.unique hgf.le âŸ¨hgâŸ©âŸ©

@[simp, norm_cast]
theorem mem_coe : s âˆˆ (f : Filter Î±) â†” s âˆˆ f :=
  Iff.rfl

theorem coe_injective : Injective ((â†‘) : Ultrafilter Î± â†’ Filter Î±)
  | âŸ¨f, hâ‚, hâ‚‚âŸ©, âŸ¨g, _, _âŸ©, _ => by congr

theorem eq_of_le {f g : Ultrafilter Î±} (h : (f : Filter Î±) â‰¤ g) : f = g :=
  coe_injective (g.unique h)

@[simp, norm_cast]
theorem coe_le_coe {f g : Ultrafilter Î±} : (f : Filter Î±) â‰¤ g â†” f = g :=
  âŸ¨fun h => eq_of_le h, fun h => h â–¸ le_rflâŸ©

@[simp, norm_cast]
theorem coe_inj : (f : Filter Î±) = g â†” f = g :=
  coe_injective.eq_iff

@[ext]
theorem ext â¦ƒf g : Ultrafilter Î±â¦„ (h : âˆ€ s, s âˆˆ f â†” s âˆˆ g) : f = g :=
  coe_injective <| Filter.ext h

theorem le_of_inf_neBot (f : Ultrafilter Î±) {g : Filter Î±} (hg : NeBot (â†‘f âŠ“ g)) : â†‘f â‰¤ g :=
  le_of_inf_eq (f.unique inf_le_left hg)

theorem le_of_inf_neBot' (f : Ultrafilter Î±) {g : Filter Î±} (hg : NeBot (g âŠ“ f)) : â†‘f â‰¤ g :=
  f.le_of_inf_neBot <| by rwa [inf_comm]

theorem inf_neBot_iff {f : Ultrafilter Î±} {g : Filter Î±} : NeBot (â†‘f âŠ“ g) â†” â†‘f â‰¤ g :=
  âŸ¨le_of_inf_neBot f, fun h => (inf_of_le_left h).symm â–¸ f.neBotâŸ©

theorem disjoint_iff_not_le {f : Ultrafilter Î±} {g : Filter Î±} : Disjoint (â†‘f) g â†” Â¬â†‘f â‰¤ g := by
  rw [â† inf_neBot_iff, neBot_iff, Ne, not_not, disjoint_iff]

@[simp]
theorem compl_notMem_iff : sá¶œ âˆ‰ f â†” s âˆˆ f :=
  âŸ¨fun hsc =>
    le_principal_iff.1 <|
      f.le_of_inf_neBot âŸ¨fun h => hsc <| mem_of_eq_bot <| by rwa [compl_compl]âŸ©,
    compl_notMemâŸ©

@[deprecated (since := "2025-05-23")] alias compl_not_mem_iff := compl_notMem_iff

@[simp]
theorem frequently_iff_eventually : (âˆƒá¶  x in f, p x) â†” âˆ€á¶  x in f, p x :=
  compl_notMem_iff

alias âŸ¨_root_.Filter.Frequently.eventually, _âŸ© := frequently_iff_eventually

theorem compl_mem_iff_notMem : sá¶œ âˆˆ f â†” s âˆ‰ f := by rw [â† compl_notMem_iff, compl_compl]

@[deprecated (since := "2025-05-23")] alias compl_mem_iff_not_mem := compl_mem_iff_notMem

theorem diff_mem_iff (f : Ultrafilter Î±) : s \ t âˆˆ f â†” s âˆˆ f âˆ§ t âˆ‰ f :=
  inter_mem_iff.trans <| and_congr Iff.rfl compl_mem_iff_notMem

/-- If `sá¶œ âˆ‰ f â†” s âˆˆ f`, then `f` is an ultrafilter. The other implication is given by
`Ultrafilter.compl_notMem_iff`. -/
def ofComplNotMemIff (f : Filter Î±) (h : âˆ€ s, sá¶œ âˆ‰ f â†” s âˆˆ f) : Ultrafilter Î± where
  toFilter := f
  neBot' := âŸ¨fun hf => by simp [hf] at hâŸ©
  le_of_le _ _ hgf s hs := (h s).1 fun hsc => compl_notMem hs (hgf hsc)

/-- If `f : Filter Î±` is an atom, then it is an ultrafilter. -/
def ofAtom (f : Filter Î±) (hf : IsAtom f) : Ultrafilter Î± where
  toFilter := f
  neBot' := âŸ¨hf.1âŸ©
  le_of_le g hg := (isAtom_iff_le_of_ge.1 hf).2 g hg.ne

theorem nonempty_of_mem (hs : s âˆˆ f) : s.Nonempty :=
  Filter.nonempty_of_mem hs

theorem ne_empty_of_mem (hs : s âˆˆ f) : s â‰  âˆ… :=
  (nonempty_of_mem hs).ne_empty

@[simp]
theorem empty_notMem : âˆ… âˆ‰ f :=
  Filter.empty_notMem (f : Filter Î±)

@[deprecated (since := "2025-05-23")] alias empty_not_mem := empty_notMem

@[simp]
theorem le_sup_iff {u : Ultrafilter Î±} {f g : Filter Î±} : â†‘u â‰¤ f âŠ” g â†” â†‘u â‰¤ f âˆ¨ â†‘u â‰¤ g :=
  not_iff_not.1 <| by simp only [â† disjoint_iff_not_le, not_or, disjoint_sup_right]

@[simp]
theorem union_mem_iff : s âˆª t âˆˆ f â†” s âˆˆ f âˆ¨ t âˆˆ f := by
  simp only [â† mem_coe, â† le_principal_iff, â† sup_principal, le_sup_iff]

theorem mem_or_compl_mem (f : Ultrafilter Î±) (s : Set Î±) : s âˆˆ f âˆ¨ sá¶œ âˆˆ f :=
  or_iff_not_imp_left.2 compl_mem_iff_notMem.2

protected theorem em (f : Ultrafilter Î±) (p : Î± â†’ Prop) : (âˆ€á¶  x in f, p x) âˆ¨ âˆ€á¶  x in f, Â¬p x :=
  f.mem_or_compl_mem { x | p x }

theorem eventually_or : (âˆ€á¶  x in f, p x âˆ¨ q x) â†” (âˆ€á¶  x in f, p x) âˆ¨ âˆ€á¶  x in f, q x :=
  union_mem_iff

theorem eventually_not : (âˆ€á¶  x in f, Â¬p x) â†” Â¬âˆ€á¶  x in f, p x :=
  compl_mem_iff_notMem

theorem eventually_imp : (âˆ€á¶  x in f, p x â†’ q x) â†” (âˆ€á¶  x in f, p x) â†’ âˆ€á¶  x in f, q x := by
  simp only [imp_iff_not_or, eventually_or, eventually_not]

/-- Pushforward for ultrafilters. -/
nonrec def map (m : Î± â†’ Î²) (f : Ultrafilter Î±) : Ultrafilter Î² :=
  ofComplNotMemIff (map m f) fun s => @compl_notMem_iff _ f (m â»Â¹' s)

@[simp, norm_cast]
theorem coe_map (m : Î± â†’ Î²) (f : Ultrafilter Î±) : (map m f : Filter Î²) = Filter.map m â†‘f :=
  rfl

@[simp]
theorem mem_map {m : Î± â†’ Î²} {f : Ultrafilter Î±} {s : Set Î²} : s âˆˆ map m f â†” m â»Â¹' s âˆˆ f :=
  Iff.rfl

@[simp]
nonrec theorem map_id (f : Ultrafilter Î±) : f.map id = f :=
  coe_injective map_id

@[simp]
theorem map_id' (f : Ultrafilter Î±) : (f.map fun x => x) = f :=
  map_id _

@[simp]
nonrec theorem map_map (f : Ultrafilter Î±) (m : Î± â†’ Î²) (n : Î² â†’ Î³) :
  (f.map m).map n = f.map (n âˆ˜ m) :=
  coe_injective map_map

/-- The pullback of an ultrafilter along an injection whose range is large with respect to the given
ultrafilter. -/
nonrec def comap {m : Î± â†’ Î²} (u : Ultrafilter Î²) (inj : Injective m) (large : Set.range m âˆˆ u) :
    Ultrafilter Î± where
  toFilter := comap m u
  neBot' := u.neBot'.comap_of_range_mem large
  le_of_le g hg hgu := by
    simp only [â† u.unique (map_le_iff_le_comap.2 hgu), comap_map inj, le_rfl]

@[simp]
theorem mem_comap {m : Î± â†’ Î²} (u : Ultrafilter Î²) (inj : Injective m) (large : Set.range m âˆˆ u)
    {s : Set Î±} : s âˆˆ u.comap inj large â†” m '' s âˆˆ u :=
  mem_comap_iff inj large

@[simp, norm_cast]
theorem coe_comap {m : Î± â†’ Î²} (u : Ultrafilter Î²) (inj : Injective m) (large : Set.range m âˆˆ u) :
    (u.comap inj large : Filter Î±) = Filter.comap m u :=
  rfl

@[simp]
nonrec theorem comap_id (f : Ultrafilter Î±) (hâ‚€ : Injective (id : Î± â†’ Î±) := injective_id)
    (hâ‚ : range id âˆˆ f := (by rw [range_id]; exact univ_mem)) :
    f.comap hâ‚€ hâ‚ = f :=
  coe_injective comap_id

@[simp]
nonrec theorem comap_comap (f : Ultrafilter Î³) {m : Î± â†’ Î²} {n : Î² â†’ Î³} (injâ‚€ : Injective n)
    (largeâ‚€ : range n âˆˆ f) (injâ‚ : Injective m) (largeâ‚ : range m âˆˆ f.comap injâ‚€ largeâ‚€)
    (injâ‚‚ : Injective (n âˆ˜ m) := injâ‚€.comp injâ‚)
    (largeâ‚‚ : range (n âˆ˜ m) âˆˆ f :=
      (by rw [range_comp]; exact image_mem_of_mem_comap largeâ‚€ largeâ‚)) :
    (f.comap injâ‚€ largeâ‚€).comap injâ‚ largeâ‚ = f.comap injâ‚‚ largeâ‚‚ :=
  coe_injective comap_comap

/-- The principal ultrafilter associated to a point `x`. -/
instance : Pure Ultrafilter :=
  âŸ¨fun a => ofComplNotMemIff (pure a) fun s => by simpâŸ©

@[simp]
theorem mem_pure {a : Î±} {s : Set Î±} : s âˆˆ (pure a : Ultrafilter Î±) â†” a âˆˆ s :=
  Iff.rfl

@[simp]
theorem coe_pure (a : Î±) : â†‘(pure a : Ultrafilter Î±) = (pure a : Filter Î±) :=
  rfl

@[simp]
theorem map_pure (m : Î± â†’ Î²) (a : Î±) : map m (pure a) = pure (m a) :=
  rfl

@[simp]
theorem comap_pure {m : Î± â†’ Î²} (a : Î±) (inj : Injective m) (large) :
    comap (pure <| m a) inj large = pure a :=
  coe_injective <|
    Filter.comap_pure.trans <| by
      rw [coe_pure, â† principal_singleton, â† image_singleton, preimage_image_eq _ inj]

theorem pure_injective : Injective (pure : Î± â†’ Ultrafilter Î±) := fun _ _ h =>
  Filter.pure_injective (congr_arg Ultrafilter.toFilter h :)

instance [Inhabited Î±] : Inhabited (Ultrafilter Î±) :=
  âŸ¨pure defaultâŸ©

instance [Nonempty Î±] : Nonempty (Ultrafilter Î±) :=
  Nonempty.map pure inferInstance

/-- Monadic bind for ultrafilters, coming from the one on filters
defined in terms of map and join. -/
def bind (f : Ultrafilter Î±) (m : Î± â†’ Ultrafilter Î²) : Ultrafilter Î² :=
  ofComplNotMemIff (Filter.bind â†‘f fun x => â†‘(m x)) fun s => by
    simp only [mem_bind', mem_coe, â† compl_mem_iff_notMem, compl_setOf, compl_compl]

instance instBind : Bind Ultrafilter :=
  âŸ¨@Ultrafilter.bindâŸ©

instance functor : Functor Ultrafilter where map := @Ultrafilter.map

instance monad : Monad Ultrafilter where map := @Ultrafilter.map

section

attribute [local instance] Filter.monad Filter.lawfulMonad

instance lawfulMonad : LawfulMonad Ultrafilter where
  id_map f := coe_injective (id_map f.toFilter)
  pure_bind a f := coe_injective (Filter.pure_bind a ((Ultrafilter.toFilter) âˆ˜ f))
  bind_assoc _ _ _ := coe_injective (filter_eq rfl)
  bind_pure_comp f x := coe_injective (bind_pure_comp f x.1)
  map_const := rfl
  seqLeft_eq _ _ := rfl
  seqRight_eq _ _ := rfl
  pure_seq _ _ := rfl
  bind_map _ _ := rfl

end

/-- The ultrafilter lemma: Any proper filter is contained in an ultrafilter. -/
theorem exists_le (f : Filter Î±) [h : NeBot f] : âˆƒ u : Ultrafilter Î±, â†‘u â‰¤ f :=
  let âŸ¨u, hu, hufâŸ© := (eq_bot_or_exists_atom_le f).resolve_left h.ne
  âŸ¨ofAtom u hu, hufâŸ©

alias _root_.Filter.exists_ultrafilter_le := exists_le

/-- Construct an ultrafilter extending a given filter.
  The ultrafilter lemma is the assertion that such a filter exists;
  we use the axiom of choice to pick one. -/
noncomputable def of (f : Filter Î±) [NeBot f] : Ultrafilter Î± :=
  Classical.choose (exists_le f)

theorem of_le (f : Filter Î±) [NeBot f] : â†‘(of f) â‰¤ f :=
  Classical.choose_spec (exists_le f)

theorem of_coe (f : Ultrafilter Î±) : of â†‘f = f :=
  coe_inj.1 <| f.unique (of_le f.toFilter)

end Ultrafilter

namespace Filter

variable {f : Filter Î±} {s : Set Î±} {a : Î±}

open Ultrafilter

theorem isAtom_pure : IsAtom (pure a : Filter Î±) :=
  (pure a : Ultrafilter Î±).isAtom

protected theorem NeBot.le_pure_iff (hf : f.NeBot) : f â‰¤ pure a â†” f = pure a :=
  âŸ¨Ultrafilter.unique (pure a), le_of_eqâŸ©

protected theorem NeBot.eq_pure_iff (hf : f.NeBot) {x : Î±} :
    f = pure x â†” {x} âˆˆ f := by
  rw [â† hf.le_pure_iff, le_pure_iff]

@[simp]
theorem lt_pure_iff : f < pure a â†” f = âŠ¥ :=
  isAtom_pure.lt_iff

theorem le_pure_iff' : f â‰¤ pure a â†” f = âŠ¥ âˆ¨ f = pure a :=
  isAtom_pure.le_iff

@[simp]
theorem Iic_pure (a : Î±) : Iic (pure a : Filter Î±) = {âŠ¥, pure a} :=
  isAtom_pure.Iic_eq

theorem mem_iff_ultrafilter : s âˆˆ f â†” âˆ€ g : Ultrafilter Î±, â†‘g â‰¤ f â†’ s âˆˆ g := by
  refine âŸ¨fun hf g hg => hg hf, fun H => by_contra fun hf => ?_âŸ©
  set g : Filter (sá¶œ : Set Î±) := comap (â†‘) f
  haveI : NeBot g := comap_neBot_iff_compl_range.2 (by simpa [compl_setOf] )
  simpa using H ((of g).map (â†‘)) (map_le_iff_le_comap.mpr (of_le g))

theorem le_iff_ultrafilter {fâ‚ fâ‚‚ : Filter Î±} : fâ‚ â‰¤ fâ‚‚ â†” âˆ€ g : Ultrafilter Î±, â†‘g â‰¤ fâ‚ â†’ â†‘g â‰¤ fâ‚‚ :=
  âŸ¨fun h _ hâ‚ => hâ‚.trans h, fun h _ hs => mem_iff_ultrafilter.2 fun g hg => h g hg hsâŸ©

/-- A filter equals the intersection of all the ultrafilters which contain it. -/
theorem iSup_ultrafilter_le_eq (f : Filter Î±) :
    â¨† (g : Ultrafilter Î±) (_ : g â‰¤ f), (g : Filter Î±) = f :=
  eq_of_forall_ge_iff fun f' => by simp only [iSup_le_iff, â† le_iff_ultrafilter]

theorem exists_ultrafilter_iff {f : Filter Î±} : (âˆƒ u : Ultrafilter Î±, â†‘u â‰¤ f) â†” NeBot f :=
  âŸ¨fun âŸ¨_, ufâŸ© => neBot_of_le uf, fun h => @exists_ultrafilter_le _ _ hâŸ©

theorem forall_neBot_le_iff {g : Filter Î±} {p : Filter Î± â†’ Prop} (hp : Monotone p) :
    (âˆ€ f : Filter Î±, NeBot f â†’ f â‰¤ g â†’ p f) â†” âˆ€ f : Ultrafilter Î±, â†‘f â‰¤ g â†’ p f := by
  refine âŸ¨fun H f hf => H f f.neBot hf, ?_âŸ©
  intro H f hf hfg
  exact hp (of_le f) (H _ ((of_le f).trans hfg))

end Filter

namespace Ultrafilter

variable {m : Î± â†’ Î²} {s : Set Î±} {g : Ultrafilter Î²}

theorem comap_inf_principal_neBot_of_image_mem (h : m '' s âˆˆ g) : (Filter.comap m g âŠ“ ğ“Ÿ s).NeBot :=
  Filter.comap_inf_principal_neBot_of_image_mem g.neBot h

/-- Ultrafilter extending the inf of a comapped ultrafilter and a principal ultrafilter. -/
noncomputable def ofComapInfPrincipal (h : m '' s âˆˆ g) : Ultrafilter Î± :=
  @of _ (Filter.comap m g âŠ“ ğ“Ÿ s) (comap_inf_principal_neBot_of_image_mem h)

theorem ofComapInfPrincipal_mem (h : m '' s âˆˆ g) : s âˆˆ ofComapInfPrincipal h := by
  let f := Filter.comap m g âŠ“ ğ“Ÿ s
  haveI : f.NeBot := comap_inf_principal_neBot_of_image_mem h
  have : s âˆˆ f := mem_inf_of_right (mem_principal_self s)
  exact le_def.mp (of_le _) s this

theorem ofComapInfPrincipal_eq_of_map (h : m '' s âˆˆ g) : (ofComapInfPrincipal h).map m = g := by
  let f := Filter.comap m g âŠ“ ğ“Ÿ s
  haveI : f.NeBot := comap_inf_principal_neBot_of_image_mem h
  apply eq_of_le
  calc
    Filter.map m (of f) â‰¤ Filter.map m f := map_mono (of_le _)
    _ â‰¤ (Filter.map m <| Filter.comap m g) âŠ“ Filter.map m (ğ“Ÿ s) := map_inf_le
    _ = (Filter.map m <| Filter.comap m g) âŠ“ (ğ“Ÿ <| m '' s) := by rw [map_principal]
    _ â‰¤ â†‘g âŠ“ (ğ“Ÿ <| m '' s) := inf_le_inf_right _ map_comap_le
    _ = â†‘g := inf_of_le_left (le_principal_iff.mpr h)

theorem eq_of_le_pure {X : Type _} {Î± : Filter X} (hÎ± : Î±.NeBot) {x y : X}
    (hx : Î± â‰¤ pure x) (hy : Î± â‰¤ pure y) : x = y :=
  Filter.pure_injective (hÎ±.le_pure_iff.mp hx â–¸ hÎ±.le_pure_iff.mp hy)

end Ultrafilter
