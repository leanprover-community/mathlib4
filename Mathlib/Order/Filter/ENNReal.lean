/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Topology.Instances.ENNReal

#align_import order.filter.ennreal from "leanprover-community/mathlib"@"52932b3a083d4142e78a15dc928084a22fea9ba0"

/-!
# Order properties of extended non-negative reals

This file compiles filter-related results about `â„â‰¥0âˆ` (see Data/Real/ENNReal.lean).
-/


open Filter ENNReal

namespace ENNReal

variable {Î± : Type*} {f : Filter Î±}

theorem eventually_le_limsup [CountableInterFilter f] (u : Î± â†’ â„â‰¥0âˆ) :
    âˆ€á¶  y in f, u y â‰¤ f.limsup u :=
  _root_.eventually_le_limsup
#align ennreal.eventually_le_limsup ENNReal.eventually_le_limsup

theorem limsup_eq_zero_iff [CountableInterFilter f] {u : Î± â†’ â„â‰¥0âˆ} :
    f.limsup u = 0 â†” u =á¶ [f] 0 :=
  limsup_eq_bot
#align ennreal.limsup_eq_zero_iff ENNReal.limsup_eq_zero_iff

theorem limsup_const_mul_of_ne_top {u : Î± â†’ â„â‰¥0âˆ} {a : â„â‰¥0âˆ} (ha_top : a â‰  âŠ¤) :
    (f.limsup fun x : Î± => a * u x) = a * f.limsup u := by
  by_cases ha_zero : a = 0
  -- âŠ¢ limsup (fun x => a * u x) f = a * limsup u f
  Â· simp_rw [ha_zero, zero_mul, â† ENNReal.bot_eq_zero]
    -- âŠ¢ limsup (fun x => âŠ¥) f = âŠ¥
    exact limsup_const_bot
    -- ğŸ‰ no goals
  let g := fun x : â„â‰¥0âˆ => a * x
  -- âŠ¢ limsup (fun x => a * u x) f = a * limsup u f
  have hg_bij : Function.Bijective g :=
    Function.bijective_iff_has_inverse.mpr
      âŸ¨fun x => aâ»Â¹ * x,
        âŸ¨fun x => by simp [â† mul_assoc, ENNReal.inv_mul_cancel ha_zero ha_top], fun x => by
          simp [â† mul_assoc, ENNReal.mul_inv_cancel ha_zero ha_top]âŸ©âŸ©
  have hg_mono : StrictMono g :=
    Monotone.strictMono_of_injective (fun _ _ _ => by rwa [mul_le_mul_left ha_zero ha_top]) hg_bij.1
  let g_iso := StrictMono.orderIsoOfSurjective g hg_mono hg_bij.2
  -- âŠ¢ limsup (fun x => a * u x) f = a * limsup u f
  exact (OrderIso.limsup_apply g_iso).symm
  -- ğŸ‰ no goals
#align ennreal.limsup_const_mul_of_ne_top ENNReal.limsup_const_mul_of_ne_top

theorem limsup_const_mul [CountableInterFilter f] {u : Î± â†’ â„â‰¥0âˆ} {a : â„â‰¥0âˆ} :
    f.limsup (a * u Â·) = a * f.limsup u := by
  by_cases ha_top : a â‰  âŠ¤
  -- âŠ¢ limsup (fun x => a * u x) f = a * limsup u f
  Â· exact limsup_const_mul_of_ne_top ha_top
    -- ğŸ‰ no goals
  push_neg at ha_top
  -- âŠ¢ limsup (fun x => a * u x) f = a * limsup u f
  by_cases hu : u =á¶ [f] 0
  -- âŠ¢ limsup (fun x => a * u x) f = a * limsup u f
  Â· have hau : (a * u Â·) =á¶ [f] 0 := hu.mono fun x hx => by simp [hx]
    -- âŠ¢ limsup (fun x => a * u x) f = a * limsup u f
    simp only [limsup_congr hu, limsup_congr hau, Pi.zero_apply, â† ENNReal.bot_eq_zero,
      limsup_const_bot]
    simp
    -- ğŸ‰ no goals
  Â· have hu_mul : âˆƒá¶  x : Î± in f, âŠ¤ â‰¤ ite (u x = 0) (0 : â„â‰¥0âˆ) âŠ¤ := by
      rw [EventuallyEq, not_eventually] at hu
      refine' hu.mono fun x hx => _
      rw [Pi.zero_apply] at hx
      simp [hx]
    have h_top_le : (f.limsup fun x : Î± => ite (u x = 0) (0 : â„â‰¥0âˆ) âŠ¤) = âŠ¤ :=
      eq_top_iff.mpr (le_limsup_of_frequently_le hu_mul)
    have hfu : f.limsup u â‰  0 := mt limsup_eq_zero_iff.1 hu
    -- âŠ¢ limsup (fun x => a * u x) f = a * limsup u f
    simp only [ha_top, top_mul', hfu, h_top_le]
    -- ğŸ‰ no goals
#align ennreal.limsup_const_mul ENNReal.limsup_const_mul

theorem limsup_mul_le [CountableInterFilter f] (u v : Î± â†’ â„â‰¥0âˆ) :
    f.limsup (u * v) â‰¤ f.limsup u * f.limsup v :=
  calc
    f.limsup (u * v) â‰¤ f.limsup fun x => f.limsup u * v x := by
      refine limsup_le_limsup ?_
      -- âŠ¢ u * v â‰¤á¶ [f] fun x => limsup u f * v x
      filter_upwards [@eventually_le_limsup _ f _ u] with x hx using mul_le_mul' hx le_rfl
      -- ğŸ‰ no goals
    _ = f.limsup u * f.limsup v := limsup_const_mul
#align ennreal.limsup_mul_le ENNReal.limsup_mul_le

theorem limsup_add_le [CountableInterFilter f] (u v : Î± â†’ â„â‰¥0âˆ) :
    f.limsup (u + v) â‰¤ f.limsup u + f.limsup v :=
  sInf_le ((eventually_le_limsup u).mp
    ((eventually_le_limsup v).mono fun _ hxg hxf => add_le_add hxf hxg))
#align ennreal.limsup_add_le ENNReal.limsup_add_le

theorem limsup_liminf_le_liminf_limsup {Î²} [Countable Î²] {f : Filter Î±} [CountableInterFilter f]
    {g : Filter Î²} (u : Î± â†’ Î² â†’ â„â‰¥0âˆ) :
    (f.limsup fun a : Î± => g.liminf fun b : Î² => u a b) â‰¤
      g.liminf fun b => f.limsup fun a => u a b :=
  have h1 : âˆ€á¶  a in f, âˆ€ b, u a b â‰¤ f.limsup fun a' => u a' b := by
    rw [eventually_countable_forall]
    -- âŠ¢ âˆ€ (i : Î²), âˆ€á¶  (x : Î±) in f, u x i â‰¤ limsup (fun a' => u a' i) f
    exact fun b => ENNReal.eventually_le_limsup fun a => u a b
    -- ğŸ‰ no goals
  sInf_le <| h1.mono fun x hx => Filter.liminf_le_liminf (Filter.eventually_of_forall hx)
#align ennreal.limsup_liminf_le_liminf_limsup ENNReal.limsup_liminf_le_liminf_limsup

end ENNReal
