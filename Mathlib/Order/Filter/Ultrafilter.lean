/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Jeremy Avigad, Yury Kudryashov
-/
import Mathlib.Order.Filter.Cofinite
import Mathlib.Order.ZornAtoms

#align_import order.filter.ultrafilter from "leanprover-community/mathlib"@"8631e2d5ea77f6c13054d9151d82b83069680cb1"

/-!
# Ultrafilters

An ultrafilter is a minimal (maximal in the set order) proper filter.
In this file we define

* `Ultrafilter.of`: an ultrafilter that is less than or equal to a given filter;
* `Ultrafilter`: subtype of ultrafilters;
* `pure x : Ultrafilter Î±`: `pure x` as an `Ultrafilter`;
* `Ultrafilter.map`, `Ultrafilter.bind`, `Ultrafilter.comap` : operations on ultrafilters;
* `hyperfilter`: the ultrafilter extending the cofinite filter.
-/


universe u v

variable {Î± : Type u} {Î² : Type v} {Î³ : Type*}

open Set Filter Function

open Classical Filter

/-- `Filter Î±` is an atomic type: for every filter there exists an ultrafilter that is less than or
equal to this filter. -/
instance : IsAtomic (Filter Î±) :=
  IsAtomic.of_isChain_bounded fun c hc hne hb =>
    âŸ¨sInf c, (sInf_neBot_of_directed' hne (show IsChain (Â· â‰¥ Â·) c from hc.symm).directedOn hb).ne,
      fun _ hx => sInf_le hxâŸ©

/-- An ultrafilter is a minimal (maximal in the set order) proper filter. -/
structure Ultrafilter (Î± : Type*) extends Filter Î± where
  /-- An ultrafilter is nontrivial. -/
  protected neBot' : NeBot toFilter
  /-- If `g` is a nontrivial filter that is less than or equal to an ultrafilter, then it is greater
  than or equal to the ultrafilter. -/
  protected le_of_le : âˆ€ g, Filter.NeBot g â†’ g â‰¤ toFilter â†’ toFilter â‰¤ g
#align ultrafilter Ultrafilter

namespace Ultrafilter

variable {f g : Ultrafilter Î±} {s t : Set Î±} {p q : Î± â†’ Prop}

attribute [coe] Ultrafilter.toFilter

instance : CoeTC (Ultrafilter Î±) (Filter Î±) :=
  âŸ¨Ultrafilter.toFilterâŸ©

instance : Membership (Set Î±) (Ultrafilter Î±) :=
  âŸ¨fun s f => s âˆˆ (f : Filter Î±)âŸ©

theorem unique (f : Ultrafilter Î±) {g : Filter Î±} (h : g â‰¤ f) (hne : NeBot g := by infer_instance) :
    g = f :=
  le_antisymm h <| f.le_of_le g hne h
#align ultrafilter.unique Ultrafilter.unique

instance neBot (f : Ultrafilter Î±) : NeBot (f : Filter Î±) :=
  f.neBot'
#align ultrafilter.ne_bot Ultrafilter.neBot

protected theorem isAtom (f : Ultrafilter Î±) : IsAtom (f : Filter Î±) :=
  âŸ¨f.neBot.ne, fun _ hgf => by_contra fun hg => hgf.ne <| f.unique hgf.le âŸ¨hgâŸ©âŸ©
#align ultrafilter.is_atom Ultrafilter.isAtom

@[simp, norm_cast]
theorem mem_coe : s âˆˆ (f : Filter Î±) â†” s âˆˆ f :=
  Iff.rfl
#align ultrafilter.mem_coe Ultrafilter.mem_coe

theorem coe_injective : Injective ((â†‘) : Ultrafilter Î± â†’ Filter Î±)
  | âŸ¨f, hâ‚, hâ‚‚âŸ©, âŸ¨g, _, _âŸ©, _ => by congr
                                    -- ğŸ‰ no goals
#align ultrafilter.coe_injective Ultrafilter.coe_injective

theorem eq_of_le {f g : Ultrafilter Î±} (h : (f : Filter Î±) â‰¤ g) : f = g :=
  coe_injective (g.unique h)
#align ultrafilter.eq_of_le Ultrafilter.eq_of_le

@[simp, norm_cast]
theorem coe_le_coe {f g : Ultrafilter Î±} : (f : Filter Î±) â‰¤ g â†” f = g :=
  âŸ¨fun h => eq_of_le h, fun h => h â–¸ le_rflâŸ©
#align ultrafilter.coe_le_coe Ultrafilter.coe_le_coe

@[simp, norm_cast]
theorem coe_inj : (f : Filter Î±) = g â†” f = g :=
  coe_injective.eq_iff
#align ultrafilter.coe_inj Ultrafilter.coe_inj

@[ext]
theorem ext â¦ƒf g : Ultrafilter Î±â¦„ (h : âˆ€ s, s âˆˆ f â†” s âˆˆ g) : f = g :=
  coe_injective <| Filter.ext h
#align ultrafilter.ext Ultrafilter.ext

theorem le_of_inf_neBot (f : Ultrafilter Î±) {g : Filter Î±} (hg : NeBot (â†‘f âŠ“ g)) : â†‘f â‰¤ g :=
  le_of_inf_eq (f.unique inf_le_left hg)
#align ultrafilter.le_of_inf_ne_bot Ultrafilter.le_of_inf_neBot

theorem le_of_inf_neBot' (f : Ultrafilter Î±) {g : Filter Î±} (hg : NeBot (g âŠ“ f)) : â†‘f â‰¤ g :=
  f.le_of_inf_neBot <| by rwa [inf_comm]
                          -- ğŸ‰ no goals
#align ultrafilter.le_of_inf_ne_bot' Ultrafilter.le_of_inf_neBot'

theorem inf_neBot_iff {f : Ultrafilter Î±} {g : Filter Î±} : NeBot (â†‘f âŠ“ g) â†” â†‘f â‰¤ g :=
  âŸ¨le_of_inf_neBot f, fun h => (inf_of_le_left h).symm â–¸ f.neBotâŸ©
#align ultrafilter.inf_ne_bot_iff Ultrafilter.inf_neBot_iff

theorem disjoint_iff_not_le {f : Ultrafilter Î±} {g : Filter Î±} : Disjoint (â†‘f) g â†” Â¬â†‘f â‰¤ g := by
  rw [â† inf_neBot_iff, neBot_iff, Ne.def, not_not, disjoint_iff]
  -- ğŸ‰ no goals
#align ultrafilter.disjoint_iff_not_le Ultrafilter.disjoint_iff_not_le

@[simp]
theorem compl_not_mem_iff : sá¶œ âˆ‰ f â†” s âˆˆ f :=
  âŸ¨fun hsc =>
    le_principal_iff.1 <|
      f.le_of_inf_neBot âŸ¨fun h => hsc <| mem_of_eq_bot <| by rwa [compl_compl]âŸ©,
                                                             -- ğŸ‰ no goals
    compl_not_memâŸ©
#align ultrafilter.compl_not_mem_iff Ultrafilter.compl_not_mem_iff

@[simp]
theorem frequently_iff_eventually : (âˆƒá¶  x in f, p x) â†” âˆ€á¶  x in f, p x :=
  compl_not_mem_iff
#align ultrafilter.frequently_iff_eventually Ultrafilter.frequently_iff_eventually

alias âŸ¨_root_.Filter.Frequently.eventually, _âŸ© := frequently_iff_eventually
#align filter.frequently.eventually Filter.Frequently.eventually

theorem compl_mem_iff_not_mem : sá¶œ âˆˆ f â†” s âˆ‰ f := by rw [â† compl_not_mem_iff, compl_compl]
                                                     -- ğŸ‰ no goals
#align ultrafilter.compl_mem_iff_not_mem Ultrafilter.compl_mem_iff_not_mem

theorem diff_mem_iff (f : Ultrafilter Î±) : s \ t âˆˆ f â†” s âˆˆ f âˆ§ t âˆ‰ f :=
  inter_mem_iff.trans <| and_congr Iff.rfl compl_mem_iff_not_mem
#align ultrafilter.diff_mem_iff Ultrafilter.diff_mem_iff

/-- If `sá¶œ âˆ‰ f â†” s âˆˆ f`, then `f` is an ultrafilter. The other implication is given by
`Ultrafilter.compl_not_mem_iff`.  -/
def ofComplNotMemIff (f : Filter Î±) (h : âˆ€ s, sá¶œ âˆ‰ f â†” s âˆˆ f) : Ultrafilter Î± where
  toFilter := f
  neBot' := âŸ¨fun hf => by simp [hf] at hâŸ©
                          -- ğŸ‰ no goals
  le_of_le g hg hgf s hs := (h s).1 fun hsc => compl_not_mem hs (hgf hsc)
#align ultrafilter.of_compl_not_mem_iff Ultrafilter.ofComplNotMemIff

/-- If `f : Filter Î±` is an atom, then it is an ultrafilter. -/
def ofAtom (f : Filter Î±) (hf : IsAtom f) : Ultrafilter Î± where
  toFilter := f
  neBot' := âŸ¨hf.1âŸ©
  le_of_le g hg := (isAtom_iff.1 hf).2 g hg.ne
#align ultrafilter.of_atom Ultrafilter.ofAtom

theorem nonempty_of_mem (hs : s âˆˆ f) : s.Nonempty :=
  Filter.nonempty_of_mem hs
#align ultrafilter.nonempty_of_mem Ultrafilter.nonempty_of_mem

theorem ne_empty_of_mem (hs : s âˆˆ f) : s â‰  âˆ… :=
  (nonempty_of_mem hs).ne_empty
#align ultrafilter.ne_empty_of_mem Ultrafilter.ne_empty_of_mem

@[simp]
theorem empty_not_mem : âˆ… âˆ‰ f :=
  Filter.empty_not_mem (f : Filter Î±)
#align ultrafilter.empty_not_mem Ultrafilter.empty_not_mem

@[simp]
theorem le_sup_iff {u : Ultrafilter Î±} {f g : Filter Î±} : â†‘u â‰¤ f âŠ” g â†” â†‘u â‰¤ f âˆ¨ â†‘u â‰¤ g :=
  not_iff_not.1 <| by simp only [â† disjoint_iff_not_le, not_or, disjoint_sup_right]
                      -- ğŸ‰ no goals
#align ultrafilter.le_sup_iff Ultrafilter.le_sup_iff

@[simp]
theorem union_mem_iff : s âˆª t âˆˆ f â†” s âˆˆ f âˆ¨ t âˆˆ f := by
  simp only [â† mem_coe, â† le_principal_iff, â† sup_principal, le_sup_iff]
  -- ğŸ‰ no goals
#align ultrafilter.union_mem_iff Ultrafilter.union_mem_iff

theorem mem_or_compl_mem (f : Ultrafilter Î±) (s : Set Î±) : s âˆˆ f âˆ¨ sá¶œ âˆˆ f :=
  or_iff_not_imp_left.2 compl_mem_iff_not_mem.2
#align ultrafilter.mem_or_compl_mem Ultrafilter.mem_or_compl_mem

protected theorem em (f : Ultrafilter Î±) (p : Î± â†’ Prop) : (âˆ€á¶  x in f, p x) âˆ¨ âˆ€á¶  x in f, Â¬p x :=
  f.mem_or_compl_mem { x | p x }
#align ultrafilter.em Ultrafilter.em

theorem eventually_or : (âˆ€á¶  x in f, p x âˆ¨ q x) â†” (âˆ€á¶  x in f, p x) âˆ¨ âˆ€á¶  x in f, q x :=
  union_mem_iff
#align ultrafilter.eventually_or Ultrafilter.eventually_or

theorem eventually_not : (âˆ€á¶  x in f, Â¬p x) â†” Â¬âˆ€á¶  x in f, p x :=
  compl_mem_iff_not_mem
#align ultrafilter.eventually_not Ultrafilter.eventually_not

theorem eventually_imp : (âˆ€á¶  x in f, p x â†’ q x) â†” (âˆ€á¶  x in f, p x) â†’ âˆ€á¶  x in f, q x := by
  simp only [imp_iff_not_or, eventually_or, eventually_not]
  -- ğŸ‰ no goals
#align ultrafilter.eventually_imp Ultrafilter.eventually_imp

theorem finite_sUnion_mem_iff {s : Set (Set Î±)} (hs : s.Finite) : â‹ƒâ‚€ s âˆˆ f â†” âˆƒ t âˆˆ s, t âˆˆ f :=
  Finite.induction_on hs (by simp) fun _ _ his => by
                             -- ğŸ‰ no goals
    simp [union_mem_iff, his, or_and_right, exists_or]
    -- ğŸ‰ no goals
#align ultrafilter.finite_sUnion_mem_iff Ultrafilter.finite_sUnion_mem_iff

theorem finite_biUnion_mem_iff {is : Set Î²} {s : Î² â†’ Set Î±} (his : is.Finite) :
    (â‹ƒ i âˆˆ is, s i) âˆˆ f â†” âˆƒ i âˆˆ is, s i âˆˆ f := by
  simp only [â† sUnion_image, finite_sUnion_mem_iff (his.image s), bex_image_iff]
  -- ğŸ‰ no goals
#align ultrafilter.finite_bUnion_mem_iff Ultrafilter.finite_biUnion_mem_iff

/-- Pushforward for ultrafilters. -/
nonrec def map (m : Î± â†’ Î²) (f : Ultrafilter Î±) : Ultrafilter Î² :=
  ofComplNotMemIff (map m f) fun s => @compl_not_mem_iff _ f (m â»Â¹' s)
#align ultrafilter.map Ultrafilter.map

@[simp, norm_cast]
theorem coe_map (m : Î± â†’ Î²) (f : Ultrafilter Î±) : (map m f : Filter Î²) = Filter.map m â†‘f :=
  rfl
#align ultrafilter.coe_map Ultrafilter.coe_map

@[simp]
theorem mem_map {m : Î± â†’ Î²} {f : Ultrafilter Î±} {s : Set Î²} : s âˆˆ map m f â†” m â»Â¹' s âˆˆ f :=
  Iff.rfl
#align ultrafilter.mem_map Ultrafilter.mem_map

@[simp]
nonrec theorem map_id (f : Ultrafilter Î±) : f.map id = f :=
  coe_injective map_id
#align ultrafilter.map_id Ultrafilter.map_id

@[simp]
theorem map_id' (f : Ultrafilter Î±) : (f.map fun x => x) = f :=
  map_id _
#align ultrafilter.map_id' Ultrafilter.map_id'

@[simp]
nonrec theorem map_map (f : Ultrafilter Î±) (m : Î± â†’ Î²) (n : Î² â†’ Î³) :
  (f.map m).map n = f.map (n âˆ˜ m) :=
  coe_injective map_map
#align ultrafilter.map_map Ultrafilter.map_map

/-- The pullback of an ultrafilter along an injection whose range is large with respect to the given
ultrafilter. -/
nonrec def comap {m : Î± â†’ Î²} (u : Ultrafilter Î²) (inj : Injective m) (large : Set.range m âˆˆ u) :
    Ultrafilter Î± where
  toFilter := comap m u
  neBot' := u.neBot'.comap_of_range_mem large
  le_of_le g hg hgu := by
    simp only [â† u.unique (map_le_iff_le_comap.2 hgu), comap_map inj, le_rfl]
    -- ğŸ‰ no goals
#align ultrafilter.comap Ultrafilter.comap

@[simp]
theorem mem_comap {m : Î± â†’ Î²} (u : Ultrafilter Î²) (inj : Injective m) (large : Set.range m âˆˆ u)
    {s : Set Î±} : s âˆˆ u.comap inj large â†” m '' s âˆˆ u :=
  mem_comap_iff inj large
#align ultrafilter.mem_comap Ultrafilter.mem_comap

@[simp, norm_cast]
theorem coe_comap {m : Î± â†’ Î²} (u : Ultrafilter Î²) (inj : Injective m) (large : Set.range m âˆˆ u) :
    (u.comap inj large : Filter Î±) = Filter.comap m u :=
  rfl
#align ultrafilter.coe_comap Ultrafilter.coe_comap

@[simp]
nonrec theorem comap_id (f : Ultrafilter Î±) (hâ‚€ : Injective (id : Î± â†’ Î±) := injective_id)
    (hâ‚ : range id âˆˆ f := (by rw [range_id]; exact univ_mem)) :
                              -- âŠ¢ univ âˆˆ f
                                             -- ğŸ‰ no goals
    f.comap hâ‚€ hâ‚ = f :=
  coe_injective comap_id
#align ultrafilter.comap_id Ultrafilter.comap_id

@[simp]
nonrec theorem comap_comap (f : Ultrafilter Î³) {m : Î± â†’ Î²} {n : Î² â†’ Î³} (injâ‚€ : Injective n)
    (largeâ‚€ : range n âˆˆ f) (injâ‚ : Injective m) (largeâ‚ : range m âˆˆ f.comap injâ‚€ largeâ‚€)
    (injâ‚‚ : Injective (n âˆ˜ m) := injâ‚€.comp injâ‚)
    (largeâ‚‚ : range (n âˆ˜ m) âˆˆ f :=
      (by rw [range_comp]; exact image_mem_of_mem_comap largeâ‚€ largeâ‚)) :
          -- âŠ¢ n '' range m âˆˆ f
                           -- ğŸ‰ no goals
    (f.comap injâ‚€ largeâ‚€).comap injâ‚ largeâ‚ = f.comap injâ‚‚ largeâ‚‚ :=
  coe_injective comap_comap
#align ultrafilter.comap_comap Ultrafilter.comap_comap

/-- The principal ultrafilter associated to a point `x`. -/
instance : Pure Ultrafilter :=
  âŸ¨fun a => ofComplNotMemIff (pure a) fun s => by simpâŸ©
                                                  -- ğŸ‰ no goals

@[simp]
theorem mem_pure {a : Î±} {s : Set Î±} : s âˆˆ (pure a : Ultrafilter Î±) â†” a âˆˆ s :=
  Iff.rfl
#align ultrafilter.mem_pure Ultrafilter.mem_pure

@[simp]
theorem coe_pure (a : Î±) : â†‘(pure a : Ultrafilter Î±) = (pure a : Filter Î±) :=
  rfl
#align ultrafilter.coe_pure Ultrafilter.coe_pure

@[simp]
theorem map_pure (m : Î± â†’ Î²) (a : Î±) : map m (pure a) = pure (m a) :=
  rfl
#align ultrafilter.map_pure Ultrafilter.map_pure

@[simp]
theorem comap_pure {m : Î± â†’ Î²} (a : Î±) (inj : Injective m) (large) :
    comap (pure <| m a) inj large = pure a :=
  coe_injective <|
    comap_pure.trans <| by
      rw [coe_pure, â† principal_singleton, â† image_singleton, preimage_image_eq _ inj]
      -- ğŸ‰ no goals
#align ultrafilter.comap_pure Ultrafilter.comap_pure

theorem pure_injective : Injective (pure : Î± â†’ Ultrafilter Î±) := fun _ _ h =>
  Filter.pure_injective (congr_arg Ultrafilter.toFilter h : _)
#align ultrafilter.pure_injective Ultrafilter.pure_injective

instance [Inhabited Î±] : Inhabited (Ultrafilter Î±) :=
  âŸ¨pure defaultâŸ©

instance [Nonempty Î±] : Nonempty (Ultrafilter Î±) :=
  Nonempty.map pure inferInstance

theorem eq_pure_of_finite_mem (h : s.Finite) (h' : s âˆˆ f) : âˆƒ x âˆˆ s, f = pure x := by
  rw [â† biUnion_of_singleton s] at h'
  -- âŠ¢ âˆƒ x, x âˆˆ s âˆ§ f = pure x
  rcases(Ultrafilter.finite_biUnion_mem_iff h).mp h' with âŸ¨a, has, hafâŸ©
  -- âŠ¢ âˆƒ x, x âˆˆ s âˆ§ f = pure x
  exact âŸ¨a, has, eq_of_le (Filter.le_pure_iff.2 haf)âŸ©
  -- ğŸ‰ no goals
#align ultrafilter.eq_pure_of_finite_mem Ultrafilter.eq_pure_of_finite_mem

theorem eq_pure_of_finite [Finite Î±] (f : Ultrafilter Î±) : âˆƒ a, f = pure a :=
  (eq_pure_of_finite_mem finite_univ univ_mem).imp fun _ âŸ¨_, haâŸ© => ha
#align ultrafilter.eq_pure_of_finite Ultrafilter.eq_pure_of_finite

theorem le_cofinite_or_eq_pure (f : Ultrafilter Î±) : (f : Filter Î±) â‰¤ cofinite âˆ¨ âˆƒ a, f = pure a :=
  or_iff_not_imp_left.2 fun h =>
    let âŸ¨_, hs, hfinâŸ© := Filter.disjoint_cofinite_right.1 (disjoint_iff_not_le.2 h)
    let âŸ¨a, _, hfâŸ© := eq_pure_of_finite_mem hfin hs
    âŸ¨a, hfâŸ©
#align ultrafilter.le_cofinite_or_eq_pure Ultrafilter.le_cofinite_or_eq_pure

/-- Monadic bind for ultrafilters, coming from the one on filters
defined in terms of map and join.-/
def bind (f : Ultrafilter Î±) (m : Î± â†’ Ultrafilter Î²) : Ultrafilter Î² :=
  ofComplNotMemIff (Filter.bind â†‘f fun x => â†‘(m x)) fun s => by
    simp only [mem_bind', mem_coe, â† compl_mem_iff_not_mem, compl_setOf, compl_compl]
    -- ğŸ‰ no goals
#align ultrafilter.bind Ultrafilter.bind

instance instBind : Bind Ultrafilter :=
  âŸ¨@Ultrafilter.bindâŸ©
#align ultrafilter.has_bind Ultrafilter.instBind

instance functor : Functor Ultrafilter where map := @Ultrafilter.map
#align ultrafilter.functor Ultrafilter.functor

instance monad : Monad Ultrafilter where map := @Ultrafilter.map
#align ultrafilter.monad Ultrafilter.monad

section

attribute [local instance] Filter.monad Filter.lawfulMonad

instance lawfulMonad : LawfulMonad Ultrafilter where
  id_map f := coe_injective (id_map f.toFilter)
  pure_bind a f := coe_injective (Filter.pure_bind a ((Ultrafilter.toFilter) âˆ˜ f))
  bind_assoc _ _ _ := coe_injective (filter_eq rfl)
  bind_pure_comp f x := coe_injective (bind_pure_comp f x.1)
  map_const := rfl
  seqLeft_eq _ _ := rfl
  seqRight_eq _ _ := rfl
  pure_seq _ _ := rfl
  bind_map _ _ := rfl
#align ultrafilter.is_lawful_monad Ultrafilter.lawfulMonad

end

/-- The ultrafilter lemma: Any proper filter is contained in an ultrafilter. -/
theorem exists_le (f : Filter Î±) [h : NeBot f] : âˆƒ u : Ultrafilter Î±, â†‘u â‰¤ f :=
  let âŸ¨u, hu, hufâŸ© := (eq_bot_or_exists_atom_le f).resolve_left h.ne
  âŸ¨ofAtom u hu, hufâŸ©
#align ultrafilter.exists_le Ultrafilter.exists_le

alias _root_.Filter.exists_ultrafilter_le := exists_le
#align filter.exists_ultrafilter_le Filter.exists_ultrafilter_le

/-- Construct an ultrafilter extending a given filter.
  The ultrafilter lemma is the assertion that such a filter exists;
  we use the axiom of choice to pick one. -/
noncomputable def of (f : Filter Î±) [NeBot f] : Ultrafilter Î± :=
  Classical.choose (exists_le f)
#align ultrafilter.of Ultrafilter.of

theorem of_le (f : Filter Î±) [NeBot f] : â†‘(of f) â‰¤ f :=
  Classical.choose_spec (exists_le f)
#align ultrafilter.of_le Ultrafilter.of_le

theorem of_coe (f : Ultrafilter Î±) : of â†‘f = f :=
  coe_inj.1 <| f.unique (of_le f.toFilter)
#align ultrafilter.of_coe Ultrafilter.of_coe

theorem exists_ultrafilter_of_finite_inter_nonempty (S : Set (Set Î±))
    (cond : âˆ€ T : Finset (Set Î±), (â†‘T : Set (Set Î±)) âŠ† S â†’ (â‹‚â‚€ (â†‘T : Set (Set Î±))).Nonempty) :
    âˆƒ F : Ultrafilter Î±, S âŠ† F.sets :=
  haveI : NeBot (generate S) :=
    generate_neBot_iff.2 fun _ hts ht =>
      ht.coe_toFinset â–¸ cond ht.toFinset (ht.coe_toFinset.symm â–¸ hts)
  âŸ¨of (generate S), fun _ ht => (of_le <| generate S) <| GenerateSets.basic htâŸ©
#align ultrafilter.exists_ultrafilter_of_finite_inter_nonempty Ultrafilter.exists_ultrafilter_of_finite_inter_nonempty

end Ultrafilter

namespace Filter

variable {f : Filter Î±} {s : Set Î±} {a : Î±}

open Ultrafilter

theorem isAtom_pure : IsAtom (pure a : Filter Î±) :=
  (pure a : Ultrafilter Î±).isAtom
#align filter.is_atom_pure Filter.isAtom_pure

protected theorem NeBot.le_pure_iff (hf : f.NeBot) : f â‰¤ pure a â†” f = pure a :=
  âŸ¨Ultrafilter.unique (pure a), le_of_eqâŸ©
#align filter.ne_bot.le_pure_iff Filter.NeBot.le_pure_iff

@[simp]
theorem lt_pure_iff : f < pure a â†” f = âŠ¥ :=
  isAtom_pure.lt_iff
#align filter.lt_pure_iff Filter.lt_pure_iff

theorem le_pure_iff' : f â‰¤ pure a â†” f = âŠ¥ âˆ¨ f = pure a :=
  isAtom_pure.le_iff
#align filter.le_pure_iff' Filter.le_pure_iff'

@[simp]
theorem Iic_pure (a : Î±) : Iic (pure a : Filter Î±) = {âŠ¥, pure a} :=
  isAtom_pure.Iic_eq
#align filter.Iic_pure Filter.Iic_pure

theorem mem_iff_ultrafilter : s âˆˆ f â†” âˆ€ g : Ultrafilter Î±, â†‘g â‰¤ f â†’ s âˆˆ g := by
  refine' âŸ¨fun hf g hg => hg hf, fun H => by_contra fun hf => _âŸ©
  -- âŠ¢ False
  set g : Filter (sá¶œ : Set Î±) := comap (â†‘) f
  -- âŠ¢ False
  haveI : NeBot g := comap_neBot_iff_compl_range.2 (by simpa [compl_setOf] )
  -- âŠ¢ False
  simpa using H ((of g).map (â†‘)) (map_le_iff_le_comap.mpr (of_le g))
  -- ğŸ‰ no goals
#align filter.mem_iff_ultrafilter Filter.mem_iff_ultrafilter

theorem le_iff_ultrafilter {fâ‚ fâ‚‚ : Filter Î±} : fâ‚ â‰¤ fâ‚‚ â†” âˆ€ g : Ultrafilter Î±, â†‘g â‰¤ fâ‚ â†’ â†‘g â‰¤ fâ‚‚ :=
  âŸ¨fun h _ hâ‚ => hâ‚.trans h, fun h _ hs => mem_iff_ultrafilter.2 fun g hg => h g hg hsâŸ©
#align filter.le_iff_ultrafilter Filter.le_iff_ultrafilter

/-- A filter equals the intersection of all the ultrafilters which contain it. -/
theorem iSup_ultrafilter_le_eq (f : Filter Î±) :
    â¨† (g : Ultrafilter Î±) (_ : g â‰¤ f), (g : Filter Î±) = f :=
  eq_of_forall_ge_iff fun f' => by simp only [iSup_le_iff, â† le_iff_ultrafilter]
                                   -- ğŸ‰ no goals
#align filter.supr_ultrafilter_le_eq Filter.iSup_ultrafilter_le_eq

/-- The `tendsto` relation can be checked on ultrafilters. -/
theorem tendsto_iff_ultrafilter (f : Î± â†’ Î²) (lâ‚ : Filter Î±) (lâ‚‚ : Filter Î²) :
    Tendsto f lâ‚ lâ‚‚ â†” âˆ€ g : Ultrafilter Î±, â†‘g â‰¤ lâ‚ â†’ Tendsto f g lâ‚‚ := by
  simpa only [tendsto_iff_comap] using le_iff_ultrafilter
  -- ğŸ‰ no goals
#align filter.tendsto_iff_ultrafilter Filter.tendsto_iff_ultrafilter

theorem exists_ultrafilter_iff {f : Filter Î±} : (âˆƒ u : Ultrafilter Î±, â†‘u â‰¤ f) â†” NeBot f :=
  âŸ¨fun âŸ¨_, ufâŸ© => neBot_of_le uf, fun h => @exists_ultrafilter_le _ _ hâŸ©
#align filter.exists_ultrafilter_iff Filter.exists_ultrafilter_iff

theorem forall_neBot_le_iff {g : Filter Î±} {p : Filter Î± â†’ Prop} (hp : Monotone p) :
    (âˆ€ f : Filter Î±, NeBot f â†’ f â‰¤ g â†’ p f) â†” âˆ€ f : Ultrafilter Î±, â†‘f â‰¤ g â†’ p f := by
  refine' âŸ¨fun H f hf => H f f.neBot hf, _âŸ©
  -- âŠ¢ (âˆ€ (f : Ultrafilter Î±), â†‘f â‰¤ g â†’ p â†‘f) â†’ âˆ€ (f : Filter Î±), NeBot f â†’ f â‰¤ g â†’ â€¦
  intro H f hf hfg
  -- âŠ¢ p f
  exact hp (of_le f) (H _ ((of_le f).trans hfg))
  -- ğŸ‰ no goals
#align filter.forall_ne_bot_le_iff Filter.forall_neBot_le_iff

section Hyperfilter

variable (Î±) [Infinite Î±]

/-- The ultrafilter extending the cofinite filter. -/
noncomputable def hyperfilter : Ultrafilter Î± :=
  Ultrafilter.of cofinite
#align filter.hyperfilter Filter.hyperfilter

variable {Î±}

theorem hyperfilter_le_cofinite : â†‘(hyperfilter Î±) â‰¤ @cofinite Î± :=
  Ultrafilter.of_le cofinite
#align filter.hyperfilter_le_cofinite Filter.hyperfilter_le_cofinite

theorem _root_.Nat.hyperfilter_le_atTop : (hyperfilter â„•).toFilter â‰¤ atTop :=
  hyperfilter_le_cofinite.trans_eq Nat.cofinite_eq_atTop

@[simp]
theorem bot_ne_hyperfilter : (âŠ¥ : Filter Î±) â‰  hyperfilter Î± :=
  (NeBot.ne inferInstance).symm
#align filter.bot_ne_hyperfilter Filter.bot_ne_hyperfilter

theorem nmem_hyperfilter_of_finite {s : Set Î±} (hf : s.Finite) : s âˆ‰ hyperfilter Î± := fun hy =>
  compl_not_mem hy <| hyperfilter_le_cofinite hf.compl_mem_cofinite
#align filter.nmem_hyperfilter_of_finite Filter.nmem_hyperfilter_of_finite

alias _root_.Set.Finite.nmem_hyperfilter := nmem_hyperfilter_of_finite
#align set.finite.nmem_hyperfilter Set.Finite.nmem_hyperfilter

theorem compl_mem_hyperfilter_of_finite {s : Set Î±} (hf : Set.Finite s) : sá¶œ âˆˆ hyperfilter Î± :=
  compl_mem_iff_not_mem.2 hf.nmem_hyperfilter
#align filter.compl_mem_hyperfilter_of_finite Filter.compl_mem_hyperfilter_of_finite

alias _root_.Set.Finite.compl_mem_hyperfilter := compl_mem_hyperfilter_of_finite
#align set.finite.compl_mem_hyperfilter Set.Finite.compl_mem_hyperfilter

theorem mem_hyperfilter_of_finite_compl {s : Set Î±} (hf : Set.Finite sá¶œ) : s âˆˆ hyperfilter Î± :=
  compl_compl s â–¸ hf.compl_mem_hyperfilter
#align filter.mem_hyperfilter_of_finite_compl Filter.mem_hyperfilter_of_finite_compl

end Hyperfilter

end Filter

namespace Ultrafilter

open Filter

variable {m : Î± â†’ Î²} {s : Set Î±} {g : Ultrafilter Î²}

theorem comap_inf_principal_neBot_of_image_mem (h : m '' s âˆˆ g) : (Filter.comap m g âŠ“ ğ“Ÿ s).NeBot :=
  Filter.comap_inf_principal_neBot_of_image_mem g.neBot h
#align ultrafilter.comap_inf_principal_ne_bot_of_image_mem Ultrafilter.comap_inf_principal_neBot_of_image_mem

/-- Ultrafilter extending the inf of a comapped ultrafilter and a principal ultrafilter. -/
noncomputable def ofComapInfPrincipal (h : m '' s âˆˆ g) : Ultrafilter Î± :=
  @of _ (Filter.comap m g âŠ“ ğ“Ÿ s) (comap_inf_principal_neBot_of_image_mem h)
#align ultrafilter.of_comap_inf_principal Ultrafilter.ofComapInfPrincipal

theorem ofComapInfPrincipal_mem (h : m '' s âˆˆ g) : s âˆˆ ofComapInfPrincipal h := by
  let f := Filter.comap m g âŠ“ ğ“Ÿ s
  -- âŠ¢ s âˆˆ ofComapInfPrincipal h
  haveI : f.NeBot := comap_inf_principal_neBot_of_image_mem h
  -- âŠ¢ s âˆˆ ofComapInfPrincipal h
  have : s âˆˆ f := mem_inf_of_right (mem_principal_self s)
  -- âŠ¢ s âˆˆ ofComapInfPrincipal h
  exact le_def.mp (of_le _) s this
  -- ğŸ‰ no goals
#align ultrafilter.of_comap_inf_principal_mem Ultrafilter.ofComapInfPrincipal_mem

theorem ofComapInfPrincipal_eq_of_map (h : m '' s âˆˆ g) : (ofComapInfPrincipal h).map m = g := by
  let f := Filter.comap m g âŠ“ ğ“Ÿ s
  -- âŠ¢ map m (ofComapInfPrincipal h) = g
  haveI : f.NeBot := comap_inf_principal_neBot_of_image_mem h
  -- âŠ¢ map m (ofComapInfPrincipal h) = g
  apply eq_of_le
  -- âŠ¢ â†‘(map m (ofComapInfPrincipal h)) â‰¤ â†‘g
  calc
    Filter.map m (of f) â‰¤ Filter.map m f := map_mono (of_le _)
    _ â‰¤ (Filter.map m <| Filter.comap m g) âŠ“ Filter.map m (ğ“Ÿ s) := map_inf_le
    _ = (Filter.map m <| Filter.comap m g) âŠ“ (ğ“Ÿ <| m '' s) := by rw [map_principal]
    _ â‰¤ â†‘g âŠ“ (ğ“Ÿ <| m '' s) := inf_le_inf_right _ map_comap_le
    _ = â†‘g := inf_of_le_left (le_principal_iff.mpr h)
#align ultrafilter.of_comap_inf_principal_eq_of_map Ultrafilter.ofComapInfPrincipal_eq_of_map

end Ultrafilter
