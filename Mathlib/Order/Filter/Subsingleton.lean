/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Order.Filter.Bases
import Mathlib.Order.Filter.Ultrafilter
/-!
# Subsingleton filters

We say that a filter `l` is a *subsingleton* if there exists a subsingleton set `s âˆˆ l`.
Equivalently, `l` is either `âŠ¥` or `pure a` for some `a`.
-/

open Set
variable {Î± Î² : Type*} {l : Filter Î±}

namespace Filter

/-- We say that a filter is a *subsingleton* if there exists a subsingleton set
that belongs to the filter. -/
protected def Subsingleton (l : Filter Î±) : Prop := âˆƒ s âˆˆ l, Set.Subsingleton s

theorem HasBasis.subsingleton_iff {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±} (h : l.HasBasis p s) :
    l.Subsingleton â†” âˆƒ i, p i âˆ§ (s i).Subsingleton :=
  h.exists_iff fun _ _ hsub h â†¦ h.anti hsub

theorem Subsingleton.anti {l'} (hl : l.Subsingleton) (hl' : l' â‰¤ l) : l'.Subsingleton :=
  let âŸ¨s, hsl, hsâŸ© := hl; âŸ¨s, hl' hsl, hsâŸ©

@[nontriviality]
theorem Subsingleton.of_subsingleton [Subsingleton Î±] : l.Subsingleton :=
  âŸ¨univ, univ_mem, subsingleton_univâŸ©

theorem Subsingleton.map (hl : l.Subsingleton) (f : Î± â†’ Î²) : (map f l).Subsingleton :=
  let âŸ¨s, hsl, hsâŸ© := hl; âŸ¨f '' s, image_mem_map hsl, hs.image fâŸ©

theorem Subsingleton.prod (hl : l.Subsingleton) {l' : Filter Î²} (hl' : l'.Subsingleton) :
    (l Ã—Ë¢ l').Subsingleton :=
  let âŸ¨s, hsl, hsâŸ© := hl; let âŸ¨t, htl', htâŸ© := hl'; âŸ¨s Ã—Ë¢ t, prod_mem_prod hsl htl', hs.prod htâŸ©

@[simp]
theorem subsingleton_pure {a : Î±} : Filter.Subsingleton (pure a) :=
  âŸ¨{a}, rfl, subsingleton_singletonâŸ©

@[simp]
theorem subsingleton_bot : Filter.Subsingleton (âŠ¥ : Filter Î±) :=
  âŸ¨âˆ…, trivial, subsingleton_emptyâŸ©

/-- A nontrivial subsingleton filter is equal to `pure a` for some `a`. -/
theorem Subsingleton.exists_eq_pure [l.NeBot] (hl : l.Subsingleton) : âˆƒ a, l = pure a := by
  rcases hl with âŸ¨s, hsl, hsâŸ©
  -- âŠ¢ âˆƒ a, l = pure a
  rcases exists_eq_singleton_iff_nonempty_subsingleton.2 âŸ¨nonempty_of_mem hsl, hsâŸ© with âŸ¨a, rflâŸ©
  -- âŠ¢ âˆƒ a, l = pure a
  refine âŸ¨a, (NeBot.le_pure_iff â€¹_â€º).1 ?_âŸ©
  -- âŠ¢ l â‰¤ pure a
  rwa [le_pure_iff]
  -- ğŸ‰ no goals

/-- A filter is a subsingleton iff it is equal to `âŠ¥` or to `pure a` for some `a`. -/
theorem subsingleton_iff_bot_or_pure : l.Subsingleton â†” l = âŠ¥ âˆ¨ âˆƒ a, l = pure a := by
  refine âŸ¨fun hl â†¦ ?_, ?_âŸ©
  -- âŠ¢ l = âŠ¥ âˆ¨ âˆƒ a, l = pure a
  Â· exact (eq_or_neBot l).imp_right (@Subsingleton.exists_eq_pure _ _ Â· hl)
    -- ğŸ‰ no goals
  Â· rintro (rfl | âŸ¨a, rflâŸ©) <;> simp
    -- âŠ¢ Filter.Subsingleton âŠ¥
                                -- ğŸ‰ no goals
                                -- ğŸ‰ no goals

/-- In a nonempty type, a filter is a subsingleton iff
it is less than or equal to a pure filter. -/
theorem subsingleton_iff_exists_le_pure [Nonempty Î±] : l.Subsingleton â†” âˆƒ a, l â‰¤ pure a := by
  rcases eq_or_neBot l with rfl | hbot
  -- âŠ¢ Filter.Subsingleton âŠ¥ â†” âˆƒ a, âŠ¥ â‰¤ pure a
  Â· simp
    -- ğŸ‰ no goals
  Â· simp [subsingleton_iff_bot_or_pure, â† hbot.le_pure_iff, hbot.ne]
    -- ğŸ‰ no goals

theorem subsingleton_iff_exists_singleton_mem [Nonempty Î±] : l.Subsingleton â†” âˆƒ a, {a} âˆˆ l := by
  simp only [subsingleton_iff_exists_le_pure, le_pure_iff]
  -- ğŸ‰ no goals

/-- A subsingleton filter on a nonempty type is less than or equal to `pure a` for some `a`. -/
alias âŸ¨Subsingleton.exists_le_pure, _âŸ© := subsingleton_iff_exists_le_pure
