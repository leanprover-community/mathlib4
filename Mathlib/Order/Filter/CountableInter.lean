/-
Copyright (c) 2020 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import Mathlib.Order.Filter.Curry
import Mathlib.Data.Set.Countable

#align_import order.filter.countable_Inter from "leanprover-community/mathlib"@"b9e46fe101fc897fb2e7edaf0bf1f09ea49eb81a"

/-!
# Filters with countable intersection property

In this file we define `CountableInterFilter` to be the class of filters with the following
property: for any countable collection of sets `s âˆˆ l` their intersection belongs to `l` as well.

Two main examples are the `residual` filter defined in `Mathlib.Topology.GDelta` and
the `MeasureTheory.ae` filter defined in `Mathlib/MeasureTheory.OuterMeasure/AE`.

We reformulate the definition in terms of indexed intersection and in terms of `Filter.Eventually`
and provide instances for some basic constructions (`âŠ¥`, `âŠ¤`, `Filter.principal`, `Filter.map`,
`Filter.comap`, `Inf.inf`). We also provide a custom constructor `Filter.ofCountableInter`
that deduces two axioms of a `Filter` from the countable intersection property.

Note that there also exists a typeclass `CardinalInterFilter`, and thus an alternative spelling of
`CountableInterFilter` as `CardinalInterFilter l (aleph 1)`. The former (defined here) is the
preferred spelling; it has the advantage of not requiring the user to import the theory ordinals.

## Tags
filter, countable
-/


open Set Filter

open Filter

variable {Î¹ : Sort*} {Î± Î² : Type*}

/-- A filter `l` has the countable intersection property if for any countable collection
of sets `s âˆˆ l` their intersection belongs to `l` as well. -/
class CountableInterFilter (l : Filter Î±) : Prop where
  /-- For a countable collection of sets `s âˆˆ l`, their intersection belongs to `l` as well. -/
  countable_sInter_mem : âˆ€ S : Set (Set Î±), S.Countable â†’ (âˆ€ s âˆˆ S, s âˆˆ l) â†’ â‹‚â‚€ S âˆˆ l
#align countable_Inter_filter CountableInterFilter

variable {l : Filter Î±} [CountableInterFilter l]

theorem countable_sInter_mem {S : Set (Set Î±)} (hSc : S.Countable) : â‹‚â‚€ S âˆˆ l â†” âˆ€ s âˆˆ S, s âˆˆ l :=
  âŸ¨fun hS _s hs => mem_of_superset hS (sInter_subset_of_mem hs),
    CountableInterFilter.countable_sInter_mem _ hScâŸ©
#align countable_sInter_mem countable_sInter_mem

theorem countable_iInter_mem [Countable Î¹] {s : Î¹ â†’ Set Î±} : (â‹‚ i, s i) âˆˆ l â†” âˆ€ i, s i âˆˆ l :=
  sInter_range s â–¸ (countable_sInter_mem (countable_range _)).trans forall_mem_range
#align countable_Inter_mem countable_iInter_mem

theorem countable_bInter_mem {Î¹ : Type*} {S : Set Î¹} (hS : S.Countable) {s : âˆ€ i âˆˆ S, Set Î±} :
    (â‹‚ i, â‹‚ hi : i âˆˆ S, s i â€¹_â€º) âˆˆ l â†” âˆ€ i, âˆ€ hi : i âˆˆ S, s i â€¹_â€º âˆˆ l := by
  rw [biInter_eq_iInter]
  haveI := hS.toEncodable
  exact countable_iInter_mem.trans Subtype.forall
#align countable_bInter_mem countable_bInter_mem

theorem eventually_countable_forall [Countable Î¹] {p : Î± â†’ Î¹ â†’ Prop} :
    (âˆ€á¶  x in l, âˆ€ i, p x i) â†” âˆ€ i, âˆ€á¶  x in l, p x i := by
  simpa only [Filter.Eventually, setOf_forall] using
    @countable_iInter_mem _ _ l _ _ fun i => { x | p x i }
#align eventually_countable_forall eventually_countable_forall

theorem eventually_countable_ball {Î¹ : Type*} {S : Set Î¹} (hS : S.Countable)
    {p : Î± â†’ âˆ€ i âˆˆ S, Prop} :
    (âˆ€á¶  x in l, âˆ€ i hi, p x i hi) â†” âˆ€ i hi, âˆ€á¶  x in l, p x i hi := by
  simpa only [Filter.Eventually, setOf_forall] using
    @countable_bInter_mem _ l _ _ _ hS fun i hi => { x | p x i hi }
#align eventually_countable_ball eventually_countable_ball

theorem EventuallyLE.countable_iUnion [Countable Î¹] {s t : Î¹ â†’ Set Î±} (h : âˆ€ i, s i â‰¤á¶ [l] t i) :
    â‹ƒ i, s i â‰¤á¶ [l] â‹ƒ i, t i :=
  (eventually_countable_forall.2 h).mono fun _ hst hs => mem_iUnion.2 <| (mem_iUnion.1 hs).imp hst
#align eventually_le.countable_Union EventuallyLE.countable_iUnion

theorem EventuallyEq.countable_iUnion [Countable Î¹] {s t : Î¹ â†’ Set Î±} (h : âˆ€ i, s i =á¶ [l] t i) :
    â‹ƒ i, s i =á¶ [l] â‹ƒ i, t i :=
  (EventuallyLE.countable_iUnion fun i => (h i).le).antisymm
    (EventuallyLE.countable_iUnion fun i => (h i).symm.le)
#align eventually_eq.countable_Union EventuallyEq.countable_iUnion

theorem EventuallyLE.countable_bUnion {Î¹ : Type*} {S : Set Î¹} (hS : S.Countable)
    {s t : âˆ€ i âˆˆ S, Set Î±} (h : âˆ€ i hi, s i hi â‰¤á¶ [l] t i hi) :
    â‹ƒ i âˆˆ S, s i â€¹_â€º â‰¤á¶ [l] â‹ƒ i âˆˆ S, t i â€¹_â€º := by
  simp only [biUnion_eq_iUnion]
  haveI := hS.toEncodable
  exact EventuallyLE.countable_iUnion fun i => h i i.2
#align eventually_le.countable_bUnion EventuallyLE.countable_bUnion

theorem EventuallyEq.countable_bUnion {Î¹ : Type*} {S : Set Î¹} (hS : S.Countable)
    {s t : âˆ€ i âˆˆ S, Set Î±} (h : âˆ€ i hi, s i hi =á¶ [l] t i hi) :
    â‹ƒ i âˆˆ S, s i â€¹_â€º =á¶ [l] â‹ƒ i âˆˆ S, t i â€¹_â€º :=
  (EventuallyLE.countable_bUnion hS fun i hi => (h i hi).le).antisymm
    (EventuallyLE.countable_bUnion hS fun i hi => (h i hi).symm.le)
#align eventually_eq.countable_bUnion EventuallyEq.countable_bUnion

theorem EventuallyLE.countable_iInter [Countable Î¹] {s t : Î¹ â†’ Set Î±} (h : âˆ€ i, s i â‰¤á¶ [l] t i) :
    â‹‚ i, s i â‰¤á¶ [l] â‹‚ i, t i :=
  (eventually_countable_forall.2 h).mono fun _ hst hs =>
    mem_iInter.2 fun i => hst _ (mem_iInter.1 hs i)
#align eventually_le.countable_Inter EventuallyLE.countable_iInter

theorem EventuallyEq.countable_iInter [Countable Î¹] {s t : Î¹ â†’ Set Î±} (h : âˆ€ i, s i =á¶ [l] t i) :
    â‹‚ i, s i =á¶ [l] â‹‚ i, t i :=
  (EventuallyLE.countable_iInter fun i => (h i).le).antisymm
    (EventuallyLE.countable_iInter fun i => (h i).symm.le)
#align eventually_eq.countable_Inter EventuallyEq.countable_iInter

theorem EventuallyLE.countable_bInter {Î¹ : Type*} {S : Set Î¹} (hS : S.Countable)
    {s t : âˆ€ i âˆˆ S, Set Î±} (h : âˆ€ i hi, s i hi â‰¤á¶ [l] t i hi) :
    â‹‚ i âˆˆ S, s i â€¹_â€º â‰¤á¶ [l] â‹‚ i âˆˆ S, t i â€¹_â€º := by
  simp only [biInter_eq_iInter]
  haveI := hS.toEncodable
  exact EventuallyLE.countable_iInter fun i => h i i.2
#align eventually_le.countable_bInter EventuallyLE.countable_bInter

theorem EventuallyEq.countable_bInter {Î¹ : Type*} {S : Set Î¹} (hS : S.Countable)
    {s t : âˆ€ i âˆˆ S, Set Î±} (h : âˆ€ i hi, s i hi =á¶ [l] t i hi) :
    â‹‚ i âˆˆ S, s i â€¹_â€º =á¶ [l] â‹‚ i âˆˆ S, t i â€¹_â€º :=
  (EventuallyLE.countable_bInter hS fun i hi => (h i hi).le).antisymm
    (EventuallyLE.countable_bInter hS fun i hi => (h i hi).symm.le)
#align eventually_eq.countable_bInter EventuallyEq.countable_bInter

/-- Construct a filter with countable intersection property. This constructor deduces
`Filter.univ_sets` and `Filter.inter_sets` from the countable intersection property. -/
def Filter.ofCountableInter (l : Set (Set Î±))
    (hl : âˆ€ S : Set (Set Î±), S.Countable â†’ S âŠ† l â†’ â‹‚â‚€ S âˆˆ l)
    (h_mono : âˆ€ s t, s âˆˆ l â†’ s âŠ† t â†’ t âˆˆ l) : Filter Î± where
  sets := l
  univ_sets := @sInter_empty Î± â–¸ hl _ countable_empty (empty_subset _)
  sets_of_superset := h_mono _ _
  inter_sets {s t} hs ht := sInter_pair s t â–¸
    hl _ ((countable_singleton _).insert _) (insert_subset_iff.2 âŸ¨hs, singleton_subset_iff.2 htâŸ©)
#align filter.of_countable_Inter Filter.ofCountableInter

instance Filter.countableInter_ofCountableInter (l : Set (Set Î±))
    (hl : âˆ€ S : Set (Set Î±), S.Countable â†’ S âŠ† l â†’ â‹‚â‚€ S âˆˆ l)
    (h_mono : âˆ€ s t, s âˆˆ l â†’ s âŠ† t â†’ t âˆˆ l) :
    CountableInterFilter (Filter.ofCountableInter l hl h_mono) :=
  âŸ¨hlâŸ©
#align filter.countable_Inter_of_countable_Inter Filter.countableInter_ofCountableInter

@[simp]
theorem Filter.mem_ofCountableInter {l : Set (Set Î±)}
    (hl : âˆ€ S : Set (Set Î±), S.Countable â†’ S âŠ† l â†’ â‹‚â‚€ S âˆˆ l) (h_mono : âˆ€ s t, s âˆˆ l â†’ s âŠ† t â†’ t âˆˆ l)
    {s : Set Î±} : s âˆˆ Filter.ofCountableInter l hl h_mono â†” s âˆˆ l :=
  Iff.rfl
#align filter.mem_of_countable_Inter Filter.mem_ofCountableInter

/-- Construct a filter with countable intersection property.
Similarly to `Filter.comk`, a set belongs to this filter if its complement satisfies the property.
Similarly to `Filter.ofCountableInter`,
this constructor deduces some properties from the countable intersection property
which becomes the countable union property because we take complements of all sets. -/
def Filter.ofCountableUnion (l : Set (Set Î±))
    (hUnion : âˆ€ S : Set (Set Î±), S.Countable â†’ (âˆ€ s âˆˆ S, s âˆˆ l) â†’ â‹ƒâ‚€ S âˆˆ l)
    (hmono : âˆ€ t âˆˆ l, âˆ€ s âŠ† t, s âˆˆ l) : Filter Î± := by
  refine .ofCountableInter {s | sá¶œ âˆˆ l} (fun S hSc hSp â†¦ ?_) fun s t ht hsub â†¦ ?_
  Â· rw [mem_setOf_eq, compl_sInter]
    apply hUnion (compl '' S) (hSc.image _)
    intro s hs
    rw [mem_image] at hs
    rcases hs with âŸ¨t, ht, rflâŸ©
    apply hSp ht
  Â· rw [mem_setOf_eq]
    rw [â† compl_subset_compl] at hsub
    exact hmono sá¶œ ht tá¶œ hsub

instance Filter.countableInter_ofCountableUnion (l : Set (Set Î±)) (hâ‚ hâ‚‚) :
    CountableInterFilter (Filter.ofCountableUnion l hâ‚ hâ‚‚) :=
  countableInter_ofCountableInter ..

@[simp]
theorem Filter.mem_ofCountableUnion {l : Set (Set Î±)} {hunion hmono s} :
    s âˆˆ ofCountableUnion l hunion hmono â†” l sá¶œ :=
  Iff.rfl

instance countableInterFilter_principal (s : Set Î±) : CountableInterFilter (ğ“Ÿ s) :=
  âŸ¨fun _ _ hS => subset_sInter hSâŸ©
#align countable_Inter_filter_principal countableInterFilter_principal

instance countableInterFilter_bot : CountableInterFilter (âŠ¥ : Filter Î±) := by
  rw [â† principal_empty]
  apply countableInterFilter_principal
#align countable_Inter_filter_bot countableInterFilter_bot

instance countableInterFilter_top : CountableInterFilter (âŠ¤ : Filter Î±) := by
  rw [â† principal_univ]
  apply countableInterFilter_principal
#align countable_Inter_filter_top countableInterFilter_top

instance (l : Filter Î²) [CountableInterFilter l] (f : Î± â†’ Î²) :
    CountableInterFilter (comap f l) := by
  refine âŸ¨fun S hSc hS => ?_âŸ©
  choose! t htl ht using hS
  have : (â‹‚ s âˆˆ S, t s) âˆˆ l := (countable_bInter_mem hSc).2 htl
  refine âŸ¨_, this, ?_âŸ©
  simpa [preimage_iInter] using iInterâ‚‚_mono ht

instance (l : Filter Î±) [CountableInterFilter l] (f : Î± â†’ Î²) : CountableInterFilter (map f l) := by
  refine âŸ¨fun S hSc hS => ?_âŸ©
  simp only [mem_map, sInter_eq_biInter, preimage_iInterâ‚‚] at hS âŠ¢
  exact (countable_bInter_mem hSc).2 hS

/-- Infimum of two `CountableInterFilter`s is a `CountableInterFilter`. This is useful, e.g.,
to automatically get an instance for `residual Î± âŠ“ ğ“Ÿ s`. -/
instance countableInterFilter_inf (lâ‚ lâ‚‚ : Filter Î±) [CountableInterFilter lâ‚]
    [CountableInterFilter lâ‚‚] : CountableInterFilter (lâ‚ âŠ“ lâ‚‚) := by
  refine âŸ¨fun S hSc hS => ?_âŸ©
  choose s hs t ht hst using hS
  replace hs : (â‹‚ i âˆˆ S, s i â€¹_â€º) âˆˆ lâ‚ := (countable_bInter_mem hSc).2 hs
  replace ht : (â‹‚ i âˆˆ S, t i â€¹_â€º) âˆˆ lâ‚‚ := (countable_bInter_mem hSc).2 ht
  refine mem_of_superset (inter_mem_inf hs ht) (subset_sInter fun i hi => ?_)
  rw [hst i hi]
  apply inter_subset_inter <;> exact iInter_subset_of_subset i (iInter_subset _ _)
#align countable_Inter_filter_inf countableInterFilter_inf

/-- Supremum of two `CountableInterFilter`s is a `CountableInterFilter`. -/
instance countableInterFilter_sup (lâ‚ lâ‚‚ : Filter Î±) [CountableInterFilter lâ‚]
    [CountableInterFilter lâ‚‚] : CountableInterFilter (lâ‚ âŠ” lâ‚‚) := by
  refine âŸ¨fun S hSc hS => âŸ¨?_, ?_âŸ©âŸ© <;> refine (countable_sInter_mem hSc).2 fun s hs => ?_
  exacts [(hS s hs).1, (hS s hs).2]
#align countable_Inter_filter_sup countableInterFilter_sup

instance CountableInterFilter.curry {Î± Î² : Type*} {l : Filter Î±} {m : Filter Î²}
    [CountableInterFilter l] [CountableInterFilter m] : CountableInterFilter (l.curry m) := âŸ¨by
  intro S Sct hS
  simp_rw [mem_curry_iff, mem_sInter, eventually_countable_ball (p := fun _ _ _ => (_ ,_) âˆˆ _) Sct,
    eventually_countable_ball (p := fun _ _ _ => âˆ€á¶  (_ : Î²) in m, _)  Sct, â† mem_curry_iff]
  exact hSâŸ©

namespace Filter

variable (g : Set (Set Î±))

/-- `Filter.CountableGenerateSets g` is the (sets of the)
greatest `countableInterFilter` containing `g`. -/
inductive CountableGenerateSets : Set Î± â†’ Prop
  | basic {s : Set Î±} : s âˆˆ g â†’ CountableGenerateSets s
  | univ : CountableGenerateSets univ
  | superset {s t : Set Î±} : CountableGenerateSets s â†’ s âŠ† t â†’ CountableGenerateSets t
  | sInter {S : Set (Set Î±)} :
    S.Countable â†’ (âˆ€ s âˆˆ S, CountableGenerateSets s) â†’ CountableGenerateSets (â‹‚â‚€ S)
#align filter.countable_generate_sets Filter.CountableGenerateSets

/-- `Filter.countableGenerate g` is the greatest `countableInterFilter` containing `g`. -/
def countableGenerate : Filter Î± :=
  ofCountableInter (CountableGenerateSets g) (fun _ => CountableGenerateSets.sInter) fun _ _ =>
    CountableGenerateSets.superset
  --deriving CountableInterFilter
#align filter.countable_generate Filter.countableGenerate

-- Porting note: could not de derived
instance : CountableInterFilter (countableGenerate g) := by
  delta countableGenerate; infer_instance

variable {g}

/-- A set is in the `countableInterFilter` generated by `g` if and only if
it contains a countable intersection of elements of `g`. -/
theorem mem_countableGenerate_iff {s : Set Î±} :
    s âˆˆ countableGenerate g â†” âˆƒ S : Set (Set Î±), S âŠ† g âˆ§ S.Countable âˆ§ â‹‚â‚€ S âŠ† s := by
  constructor <;> intro h
  Â· induction' h with s hs s t _ st ih S Sct _ ih
    Â· exact âŸ¨{s}, by simp [hs, subset_refl]âŸ©
    Â· exact âŸ¨âˆ…, by simpâŸ©
    Â· refine Exists.imp (fun S => ?_) ih
      tauto
    choose T Tg Tct hT using ih
    refine âŸ¨â‹ƒ (s) (H : s âˆˆ S), T s H, by simpa, Sct.biUnion Tct, ?_âŸ©
    apply subset_sInter
    intro s H
    exact subset_trans (sInter_subset_sInter (subset_iUnionâ‚‚ s H)) (hT s H)
  rcases h with âŸ¨S, Sg, Sct, hSâŸ©
  refine mem_of_superset ((countable_sInter_mem Sct).mpr ?_) hS
  intro s H
  exact CountableGenerateSets.basic (Sg H)
#align filter.mem_countable_generate_iff Filter.mem_countableGenerate_iff

theorem le_countableGenerate_iff_of_countableInterFilter {f : Filter Î±} [CountableInterFilter f] :
    f â‰¤ countableGenerate g â†” g âŠ† f.sets := by
  constructor <;> intro h
  Â· exact subset_trans (fun s => CountableGenerateSets.basic) h
  intro s hs
  induction' hs with s hs s t _ st ih S Sct _ ih
  Â· exact h hs
  Â· exact univ_mem
  Â· exact mem_of_superset ih st
  exact (countable_sInter_mem Sct).mpr ih
#align filter.le_countable_generate_iff_of_countable_Inter_filter Filter.le_countableGenerate_iff_of_countableInterFilter

variable (g)

/-- `countableGenerate g` is the greatest `countableInterFilter` containing `g`. -/
theorem countableGenerate_isGreatest :
    IsGreatest { f : Filter Î± | CountableInterFilter f âˆ§ g âŠ† f.sets } (countableGenerate g) := by
  refine âŸ¨âŸ¨inferInstance, fun s => CountableGenerateSets.basicâŸ©, ?_âŸ©
  rintro f âŸ¨fct, hfâŸ©
  rwa [@le_countableGenerate_iff_of_countableInterFilter _ _ _ fct]
#align filter.countable_generate_is_greatest Filter.countableGenerate_isGreatest

end Filter
