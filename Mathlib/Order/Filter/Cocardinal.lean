/-
Copyright (c) 2024 Josha Dekker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Josha Dekker
-/
import Mathlib.Order.Filter.Cofinite
import Mathlib.Order.Filter.CountableInter
import Mathlib.Order.Filter.CardinalInter
import Mathlib.SetTheory.Cardinal.Arithmetic
import Mathlib.SetTheory.Cardinal.Cofinality

/-!
# The cocardinal filter

In this file we define `Filter.cocardinal hc`: the filter of sets with cardinality less than
  a regular cardinal `c` that satisfies `Cardinal.aleph0 < c`.
  Such filters are `CardinalInterFilter` with cardinality `c`.

-/

open Set Filter Cardinal

universe u
variable {Î± : Type u} {c : Cardinal.{u}} {hreg : c.IsRegular}

namespace Filter

variable (Î±) in
/-- The filter defined by all sets that have a complement with at most cardinality `c`. For a union
of `c` sets of `c` elements to have `c` elements, we need that `c` is a regular cardinal. -/
def cocardinal (hreg : c.IsRegular) : Filter Î± := by
  apply ofCardinalUnion {s | Cardinal.mk s < c} (lt_of_lt_of_le (nat_lt_aleph0 2) hreg.aleph0_le)
  Â· refine fun s hS hSc â†¦ lt_of_le_of_lt (mk_sUnion_le _) <| mul_lt_of_lt hreg.aleph0_le hS ?_
    exact iSup_lt_of_isRegular hreg hS fun i â†¦ hSc i i.property
  Â· exact fun _ hSc _ ht â†¦ lt_of_le_of_lt (mk_le_mk_of_subset ht) hSc

@[simp]
theorem mem_cocardinal {s : Set Î±} :
    s âˆˆ cocardinal Î± hreg â†” Cardinal.mk (sá¶œ : Set Î±) < c := Iff.rfl

@[simp] lemma cocardinal_aleph0_eq_cofinite :
    cocardinal (Î± := Î±) isRegular_aleph0 = cofinite := by
  aesop

instance instCardinalInterFilter_cocardinal : CardinalInterFilter (cocardinal (Î± := Î±) hreg) c where
  cardinal_sInter_mem S hS hSs := by
    rw [mem_cocardinal, Set.compl_sInter]
    apply lt_of_le_of_lt (mk_sUnion_le _)
    apply mul_lt_of_lt hreg.aleph0_le (lt_of_le_of_lt mk_image_le hS)
    apply iSup_lt_of_isRegular hreg <| lt_of_le_of_lt mk_image_le hS
    aesop

@[simp]
theorem eventually_cocardinal {p : Î± â†’ Prop} :
    (âˆ€á¶  x in cocardinal Î± hreg, p x) â†” #{ x | Â¬p x } < c := Iff.rfl

theorem hasBasis_cocardinal : HasBasis (cocardinal Î± hreg) {s : Set Î± | #s < c} compl :=
  âŸ¨fun s =>
    âŸ¨fun h => âŸ¨sá¶œ, h, (compl_compl s).subsetâŸ©, fun âŸ¨_t, htf, htsâŸ© => by
      have : #â†‘sá¶œ < c := by
        apply lt_of_le_of_lt _ htf
        rw [compl_subset_comm] at hts
        apply Cardinal.mk_le_mk_of_subset hts
      simp_all only [mem_cocardinal] âŸ©âŸ©

theorem frequently_cocardinal {p : Î± â†’ Prop} :
    (âˆƒá¶  x in cocardinal Î± hreg, p x) â†” c â‰¤ #{ x | p x } := by
  simp only [Filter.Frequently, eventually_cocardinal, not_not, coe_setOf, not_lt]

lemma frequently_cocardinal_mem {s : Set Î±} :
    (âˆƒá¶  x in cocardinal Î± hreg, x âˆˆ s) â†” c â‰¤ #s := frequently_cocardinal

@[simp]
lemma cocardinal_inf_principal_neBot_iff {s : Set Î±} :
    (cocardinal Î± hreg âŠ“ ð“Ÿ s).NeBot â†” c â‰¤ #s :=
  frequently_mem_iff_neBot.symm.trans frequently_cocardinal

theorem compl_mem_cocardinal_of_card_lt {s : Set Î±} (hs : #s < c) :
    sá¶œ âˆˆ cocardinal Î± hreg :=
  mem_cocardinal.2 <| (compl_compl s).symm â–¸ hs

theorem _root_.Set.Finite.compl_mem_cocardinal {s : Set Î±} (hs : s.Finite) :
    sá¶œ âˆˆ cocardinal Î± hreg :=
  compl_mem_cocardinal_of_card_lt <| lt_of_lt_of_le (Finite.lt_aleph0 hs) (hreg.aleph0_le)

theorem eventually_cocardinal_notMem_of_card_lt {s : Set Î±} (hs : #s < c) :
    âˆ€á¶  x in cocardinal Î± hreg, x âˆ‰ s :=
  compl_mem_cocardinal_of_card_lt hs

@[deprecated (since := "2025-05-24")]
alias eventually_cocardinal_nmem_of_card_lt := eventually_cocardinal_notMem_of_card_lt

theorem _root_.Finset.eventually_cocardinal_notMem (s : Finset Î±) :
    âˆ€á¶  x in cocardinal Î± hreg, x âˆ‰ s :=
  eventually_cocardinal_notMem_of_card_lt <| (finset_card_lt_aleph0 s).trans_le (hreg.aleph0_le)

@[deprecated (since := "2025-05-24")]
alias _root_.Finset.eventually_cocardinal_nmem := _root_.Finset.eventually_cocardinal_notMem

theorem eventually_cocardinal_ne (x : Î±) : âˆ€á¶  a in cocardinal Î± hreg, a â‰  x := by
  simpa [Set.finite_singleton x] using hreg.nat_lt 1

/-- The filter defined by all sets that have countable complements. -/
abbrev cocountable : Filter Î± := cocardinal Î± Cardinal.isRegular_aleph_one

theorem mem_cocountable {s : Set Î±} :
    s âˆˆ cocountable â†” (sá¶œ : Set Î±).Countable := by
  rw [Cardinal.countable_iff_lt_aleph_one, mem_cocardinal]

end Filter
