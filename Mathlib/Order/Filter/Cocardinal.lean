/-
Copyright (c) 2024 Josha Dekker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Josha Dekker
-/
import Mathlib.Order.Filter.AtTopBot
import Mathlib.Order.Filter.Pi
import Mathlib.Order.Filter.CountableInter
import Mathlib.Order.Filter.CardinalInter
import Mathlib.SetTheory.Cardinal.Ordinal
import Mathlib.SetTheory.Cardinal.Cofinality

/-!
# The cocardinal filter

In this file we define `Filter.cocardinal hc`: the filter of sets with cardinality less than
  a regular cardinal `c` that satisfies `Cardinal.aleph0 < c`.
  Such filters are `CardinalInterFilter` with cardinality `c`.

-/

open Set Filter Cardinal

universe u
variable {Î¹ : Type u} {Î± Î² : Type u}
variable {c : Cardinal.{u}} {hreg : Cardinal.IsRegular c}
variable {l : Filter Î±}

namespace Filter

/-- The filter defined by all sets that have a complement with at most cardinality `c`. For a union
of `c` sets of `c` elements to have `c` elements, we need that `c` is a regular cardinal. -/
def cocardinal (hreg : Cardinal.IsRegular c) : Filter Î± := by
  have hc : Cardinal.aleph0 â‰¤ c := Cardinal.IsRegular.aleph0_le hreg
  apply ofCardinalUnion (fun s â†¦ Cardinal.mk s < c) (lt_of_lt_of_le (Cardinal.nat_lt_aleph0 2) hc)
  Â· intro s hS hSc
    apply lt_of_le_of_lt (Cardinal.mk_sUnion_le _)
    apply Cardinal.mul_lt_of_lt hc hS
    apply Cardinal.iSup_lt_of_isRegular hreg hS
    intro i
    simp_all only [Subtype.coe_prop]
  Â· exact fun _ hSc _ ht â†¦ lt_of_le_of_lt (Cardinal.mk_le_mk_of_subset ht) hSc

@[simp]
theorem mem_cocardinal {s : Set Î±} :
    s âˆˆ @cocardinal Î± c hreg â†” Cardinal.mk (sá¶œ : Set Î±) < c := Iff.rfl

instance cardinalInter_ofCoCardinal :
    CardinalInterFilter (@cocardinal Î± c hreg) c where
  cardinal_sInter_mem := by
    have hc : Cardinal.aleph0 â‰¤ c := Cardinal.IsRegular.aleph0_le hreg
    intro S hS hSs
    rw [mem_cocardinal]
    have := fun s hs => mem_cocardinal.1 (hSs s hs)
    rw [Set.compl_sInter]
    apply lt_of_le_of_lt (Cardinal.mk_sUnion_le _)
    apply Cardinal.mul_lt_of_lt hc
    Â· apply lt_of_le_of_lt Cardinal.mk_image_le hS
    Â· apply Cardinal.iSup_lt_of_isRegular hreg
      apply lt_of_le_of_lt Cardinal.mk_image_le hS
      intro i
      aesop

@[simp]
theorem eventually_cocardinal {p : Î± â†’ Prop} :
    (âˆ€á¶  x in cocardinal hreg, p x) â†” #{ x | Â¬p x } < c := Iff.rfl

theorem hasBasis_cocardinal : HasBasis (cocardinal hreg)
    (fun s : Set Î± => (#s < c)) compl :=
  âŸ¨fun s =>
    âŸ¨fun h => âŸ¨sá¶œ, h, (compl_compl s).subsetâŸ©, fun âŸ¨_t, htf, htsâŸ© => by
      have : #â†‘sá¶œ < c := by
        apply lt_of_le_of_lt _ htf
        rw [compl_subset_comm] at hts
        apply Cardinal.mk_le_mk_of_subset hts
      simp_all only [mem_cocardinal] âŸ©âŸ©

theorem frequently_cocardinal_iff_cardinal_le {p : Î± â†’ Prop} :
    (âˆƒá¶  x in cocardinal hreg, p x) â†” c â‰¤ # { x | p x }  := by
  simp only [Filter.Frequently, eventually_cocardinal, not_not,coe_setOf,
    not_lt]

lemma frequently_cocardinal_mem_iff_cardinal_le {s : Set Î±} :
    (âˆƒá¶  x in cocardinal hreg, x âˆˆ s) â†” c â‰¤ #s := frequently_cocardinal_iff_cardinal_le

@[simp]
lemma cocardinal_inf_principal_neBot_iff {s : Set Î±} :
    (cocardinal hreg âŠ“ ð“Ÿ s).NeBot â†” c â‰¤ #s :=
  frequently_mem_iff_neBot.symm.trans frequently_cocardinal_iff_cardinal_le

theorem _root_.Set.compl_mem_cocardinal {s : Set Î±} (hs : #s < c) :
    sá¶œ âˆˆ @cocardinal Î± c hreg :=
  mem_cocardinal.2 <| (compl_compl s).symm â–¸ hs

theorem _root_.Set.Finite.compl_mem_cocardinal {s : Set Î±} (hs : s.Finite) :
    sá¶œ âˆˆ @cocardinal Î± c hreg := by
  have : #s < c := lt_of_lt_of_le (Finite.lt_aleph0 hs) (Cardinal.IsRegular.aleph0_le hreg)
  exact Set.compl_mem_cocardinal this

theorem _root_.Set.eventually_cocardinal_nmem {s : Set Î±} (hs : #s < c) :
    âˆ€á¶  x in cocardinal hreg, x âˆ‰ s :=
  s.compl_mem_cocardinal hs

theorem _root_.Finset.eventually_cocardinal_nmem (s : Finset Î±) :
    âˆ€á¶  x in cocardinal hreg, x âˆ‰ s := by
  have : #s < c := lt_of_lt_of_le (finset_card_lt_aleph0 s) (Cardinal.IsRegular.aleph0_le hreg)
  exact Set.eventually_cocardinal_nmem this

theorem _root_.Set.cardinal_iff_frequently_cocardinal {s : Set Î±} :
    c â‰¤ #s â†” âˆƒá¶  x in cocardinal hreg, x âˆˆ s :=
  frequently_cocardinal_iff_cardinal_le.symm

theorem eventually_cocardinal_ne (x : Î±) : âˆ€á¶  a in cocardinal hreg, a â‰  x := by
  simp [Set.finite_singleton x]
  exact IsRegular.nat_lt hreg 1

abbrev cocountable : Filter Î± := cocardinal Cardinal.isRegular_aleph_one

@[simp]
theorem mem_cocountable {s : Set Î±} :
    s âˆˆ @cocountable Î± â†” (sá¶œ : Set Î±).Countable := by
  rw [Cardinal.countable_iff_lt_aleph_one, mem_cocardinal]
