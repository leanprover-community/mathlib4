/-
Copyright (c) 2018 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Johannes HÃ¶lzl, RÃ©my Degenne
-/
import Mathlib.Algebra.BigOperators.Group.Finset.Basic
import Mathlib.Algebra.Order.Group.Unbundled.Abs
import Mathlib.Algebra.Order.GroupWithZero.Unbundled.Defs
import Mathlib.Algebra.Order.Monoid.Defs
import Mathlib.Order.Filter.Cofinite

/-!
# Lemmas about `Is(Co)Bounded(Under)`

This file proves several lemmas about
`IsBounded`, `IsBoundedUnder`, `IsCobounded` and `IsCoboundedUnder`.
-/

open Set Function

variable {Î± Î² Î³ Î¹ : Type*}

namespace Filter

section Relation

variable {r : Î± â†’ Î± â†’ Prop} {f g : Filter Î±}

/-- `f` is eventually bounded if and only if, there exists an admissible set on which it is
bounded. -/
theorem isBounded_iff : f.IsBounded r â†” âˆƒ s âˆˆ f.sets, âˆƒ b, s âŠ† { x | r x b } :=
  Iff.intro (fun âŸ¨b, hbâŸ© => âŸ¨{ a | r a b }, hb, b, Subset.refl _âŸ©) fun âŸ¨_, hs, b, hbâŸ© =>
    âŸ¨b, mem_of_superset hs hbâŸ©

/-- A bounded function `u` is in particular eventually bounded. -/
theorem isBoundedUnder_of {f : Filter Î²} {u : Î² â†’ Î±} : (âˆƒ b, âˆ€ x, r (u x) b) â†’ f.IsBoundedUnder r u
  | âŸ¨b, hbâŸ© => âŸ¨b, show âˆ€á¶  x in f, r (u x) b from Eventually.of_forall hbâŸ©

theorem isBounded_bot : IsBounded r âŠ¥ â†” Nonempty Î± := by simp [IsBounded, exists_true_iff_nonempty]

theorem isBounded_top : IsBounded r âŠ¤ â†” âˆƒ t, âˆ€ x, r x t := by simp [IsBounded]

theorem isBounded_principal (s : Set Î±) : IsBounded r (ğ“Ÿ s) â†” âˆƒ t, âˆ€ x âˆˆ s, r x t := by
  simp [IsBounded]

theorem isBounded_sup [IsTrans Î± r] [IsDirected Î± r] :
    IsBounded r f â†’ IsBounded r g â†’ IsBounded r (f âŠ” g)
  | âŸ¨bâ‚, hâ‚âŸ©, âŸ¨bâ‚‚, hâ‚‚âŸ© =>
    let âŸ¨b, rbâ‚b, rbâ‚‚bâŸ© := directed_of r bâ‚ bâ‚‚
    âŸ¨b, eventually_sup.mpr
      âŸ¨hâ‚.mono fun _ h => _root_.trans h rbâ‚b, hâ‚‚.mono fun _ h => _root_.trans h rbâ‚‚bâŸ©âŸ©

theorem IsBounded.mono (h : f â‰¤ g) : IsBounded r g â†’ IsBounded r f
  | âŸ¨b, hbâŸ© => âŸ¨b, h hbâŸ©

theorem IsBoundedUnder.mono {f g : Filter Î²} {u : Î² â†’ Î±} (h : f â‰¤ g) :
    g.IsBoundedUnder r u â†’ f.IsBoundedUnder r u := fun hg => IsBounded.mono (map_mono h) hg

theorem IsBoundedUnder.mono_le [Preorder Î²] {l : Filter Î±} {u v : Î± â†’ Î²}
    (hu : IsBoundedUnder (Â· â‰¤ Â·) l u) (hv : v â‰¤á¶ [l] u) : IsBoundedUnder (Â· â‰¤ Â·) l v := by
  apply hu.imp
  exact fun b hb => (eventually_map.1 hb).mp <| hv.mono fun x => le_trans

theorem IsBoundedUnder.mono_ge [Preorder Î²] {l : Filter Î±} {u v : Î± â†’ Î²}
    (hu : IsBoundedUnder (Â· â‰¥ Â·) l u) (hv : u â‰¤á¶ [l] v) : IsBoundedUnder (Â· â‰¥ Â·) l v :=
  IsBoundedUnder.mono_le (Î² := Î²áµ’áµˆ) hu hv

theorem isBoundedUnder_const [IsRefl Î± r] {l : Filter Î²} {a : Î±} : IsBoundedUnder r l fun _ => a :=
  âŸ¨a, eventually_map.2 <| Eventually.of_forall fun _ => refl _âŸ©

theorem IsBounded.isBoundedUnder {q : Î² â†’ Î² â†’ Prop} {u : Î± â†’ Î²}
    (hu : âˆ€ aâ‚€ aâ‚, r aâ‚€ aâ‚ â†’ q (u aâ‚€) (u aâ‚)) : f.IsBounded r â†’ f.IsBoundedUnder q u
  | âŸ¨b, hâŸ© => âŸ¨u b, show âˆ€á¶  x in f, q (u x) (u b) from h.mono fun x => hu x bâŸ©

theorem IsBoundedUnder.comp {l : Filter Î³} {q : Î² â†’ Î² â†’ Prop} {u : Î³ â†’ Î±} {v : Î± â†’ Î²}
    (hv : âˆ€ aâ‚€ aâ‚, r aâ‚€ aâ‚ â†’ q (v aâ‚€) (v aâ‚)) : l.IsBoundedUnder r u â†’ l.IsBoundedUnder q (v âˆ˜ u)
  | âŸ¨a, hâŸ© => âŸ¨v a, show âˆ€á¶  x in map u l, q (v x) (v a) from h.mono fun x => hv x aâŸ©

lemma isBoundedUnder_map_iff {Î¹ Îº X : Type*} {r : X â†’ X â†’ Prop} {f : Î¹ â†’ X} {Ï† : Îº â†’ Î¹}
    {ğ“• : Filter Îº} :
    (map Ï† ğ“•).IsBoundedUnder r f â†” ğ“•.IsBoundedUnder r (f âˆ˜ Ï†) :=
  Iff.rfl

lemma Tendsto.isBoundedUnder_comp {Î¹ Îº X : Type*} {r : X â†’ X â†’ Prop} {f : Î¹ â†’ X} {Ï† : Îº â†’ Î¹}
    {ğ“• : Filter Î¹} {ğ“– : Filter Îº} (Ï†_tendsto : Tendsto Ï† ğ“– ğ“•) (ğ“•_bounded : ğ“•.IsBoundedUnder r f) :
    ğ“–.IsBoundedUnder r (f âˆ˜ Ï†) :=
  isBoundedUnder_map_iff.mp (ğ“•_bounded.mono Ï†_tendsto)

section Preorder
variable [Preorder Î±] {f : Filter Î²} {u : Î² â†’ Î±} {s : Set Î²}

lemma IsBoundedUnder.eventually_le (h : IsBoundedUnder (Â· â‰¤ Â·) f u) :
    âˆƒ a, âˆ€á¶  x in f, u x â‰¤ a := by
  tauto

lemma IsBoundedUnder.eventually_ge (h : IsBoundedUnder (Â· â‰¥ Â·) f u) :
    âˆƒ a, âˆ€á¶  x in f, a â‰¤ u x :=
  IsBoundedUnder.eventually_le (Î± := Î±áµ’áµˆ) h

lemma isBoundedUnder_of_eventually_le {a : Î±} (h : âˆ€á¶  x in f, u x â‰¤ a) :
    IsBoundedUnder (Â· â‰¤ Â·) f u := âŸ¨a, hâŸ©

lemma isBoundedUnder_of_eventually_ge {a : Î±} (h : âˆ€á¶  x in f, a â‰¤ u x) :
    IsBoundedUnder (Â· â‰¥ Â·) f u := âŸ¨a, hâŸ©

lemma isBoundedUnder_iff_eventually_bddAbove :
    f.IsBoundedUnder (Â· â‰¤ Â·) u â†” âˆƒ s, BddAbove (u '' s) âˆ§ âˆ€á¶  x in f, x âˆˆ s := by
  constructor
  Â· rintro âŸ¨b, hbâŸ©
    exact âŸ¨{a | u a â‰¤ b}, âŸ¨b, by rintro _ âŸ¨a, ha, rflâŸ©; exact haâŸ©, hbâŸ©
  Â· rintro âŸ¨s, âŸ¨b, hbâŸ©, hsâŸ©
    exact âŸ¨b, hs.mono <| by simpa [upperBounds] using hbâŸ©

lemma isBoundedUnder_iff_eventually_bddBelow :
    f.IsBoundedUnder (Â· â‰¥ Â·) u â†” âˆƒ s, BddBelow (u '' s) âˆ§ âˆ€á¶  x in f, x âˆˆ s :=
  isBoundedUnder_iff_eventually_bddAbove (Î± := Î±áµ’áµˆ)

lemma _root_.BddAbove.isBoundedUnder (hs : s âˆˆ f) (hu : BddAbove (u '' s)) :
    f.IsBoundedUnder (Â· â‰¤ Â·) u := isBoundedUnder_iff_eventually_bddAbove.2 âŸ¨_, hu, hsâŸ©

/-- A bounded above function `u` is in particular eventually bounded above. -/
lemma _root_.BddAbove.isBoundedUnder_of_range (hu : BddAbove (Set.range u)) :
    f.IsBoundedUnder (Â· â‰¤ Â·) u := BddAbove.isBoundedUnder (s := univ) f.univ_mem (by simpa)

lemma _root_.BddBelow.isBoundedUnder (hs : s âˆˆ f) (hu : BddBelow (u '' s)) :
    f.IsBoundedUnder (Â· â‰¥ Â·) u := isBoundedUnder_iff_eventually_bddBelow.2 âŸ¨_, hu, hsâŸ©

/-- A bounded below function `u` is in particular eventually bounded below. -/
lemma _root_.BddBelow.isBoundedUnder_of_range (hu : BddBelow (Set.range u)) :
    f.IsBoundedUnder (Â· â‰¥ Â·) u := BddBelow.isBoundedUnder (s := univ) f.univ_mem (by simpa)

lemma IsBoundedUnder.le_of_finite [Nonempty Î±] [IsDirected Î± (Â· â‰¤ Â·)] [Finite Î²]
    {f : Filter Î²} {u : Î² â†’ Î±} : IsBoundedUnder (Â· â‰¤ Â·) f u :=
  (Set.toFinite _).bddAbove.isBoundedUnder_of_range

lemma IsBoundedUnder.ge_of_finite [Nonempty Î±] [IsDirected Î± (Â· â‰¥ Â·)] [Finite Î²]
    {f : Filter Î²} {u : Î² â†’ Î±} : IsBoundedUnder (Â· â‰¥ Â·) f u :=
  (Set.toFinite _).bddBelow.isBoundedUnder_of_range

end Preorder

theorem _root_.Monotone.isBoundedUnder_le_comp [Preorder Î±] [Preorder Î²] {l : Filter Î³} {u : Î³ â†’ Î±}
    {v : Î± â†’ Î²} (hv : Monotone v) (hl : l.IsBoundedUnder (Â· â‰¤ Â·) u) :
    l.IsBoundedUnder (Â· â‰¤ Â·) (v âˆ˜ u) :=
  hl.comp hv

theorem _root_.Monotone.isBoundedUnder_ge_comp [Preorder Î±] [Preorder Î²] {l : Filter Î³} {u : Î³ â†’ Î±}
    {v : Î± â†’ Î²} (hv : Monotone v) (hl : l.IsBoundedUnder (Â· â‰¥ Â·) u) :
    l.IsBoundedUnder (Â· â‰¥ Â·) (v âˆ˜ u) :=
  hl.comp (swap hv)

theorem _root_.Antitone.isBoundedUnder_le_comp [Preorder Î±] [Preorder Î²] {l : Filter Î³} {u : Î³ â†’ Î±}
    {v : Î± â†’ Î²} (hv : Antitone v) (hl : l.IsBoundedUnder (Â· â‰¥ Â·) u) :
    l.IsBoundedUnder (Â· â‰¤ Â·) (v âˆ˜ u) :=
  hl.comp (swap hv)

theorem _root_.Antitone.isBoundedUnder_ge_comp [Preorder Î±] [Preorder Î²] {l : Filter Î³} {u : Î³ â†’ Î±}
    {v : Î± â†’ Î²} (hv : Antitone v) (hl : l.IsBoundedUnder (Â· â‰¤ Â·) u) :
    l.IsBoundedUnder (Â· â‰¥ Â·) (v âˆ˜ u) :=
  hl.comp hv

theorem not_isBoundedUnder_of_tendsto_atTop [Preorder Î²] [NoMaxOrder Î²] {f : Î± â†’ Î²} {l : Filter Î±}
    [l.NeBot] (hf : Tendsto f l atTop) : Â¬IsBoundedUnder (Â· â‰¤ Â·) l f := by
  rintro âŸ¨b, hbâŸ©
  rw [eventually_map] at hb
  obtain âŸ¨b', hâŸ© := exists_gt b
  have hb' := (tendsto_atTop.mp hf) b'
  have : { x : Î± | f x â‰¤ b } âˆ© { x : Î± | b' â‰¤ f x } = âˆ… :=
    eq_empty_of_subset_empty fun x hx => (not_le_of_gt h) (le_trans hx.2 hx.1)
  exact (nonempty_of_mem (hb.and hb')).ne_empty this

theorem not_isBoundedUnder_of_tendsto_atBot [Preorder Î²] [NoMinOrder Î²] {f : Î± â†’ Î²} {l : Filter Î±}
    [l.NeBot] (hf : Tendsto f l atBot) : Â¬IsBoundedUnder (Â· â‰¥ Â·) l f :=
  not_isBoundedUnder_of_tendsto_atTop (Î² := Î²áµ’áµˆ) hf

theorem IsBoundedUnder.bddAbove_range_of_cofinite [Preorder Î²] [IsDirected Î² (Â· â‰¤ Â·)] {f : Î± â†’ Î²}
    (hf : IsBoundedUnder (Â· â‰¤ Â·) cofinite f) : BddAbove (range f) := by
  rcases hf with âŸ¨b, hbâŸ©
  haveI : Nonempty Î² := âŸ¨bâŸ©
  rw [â† image_univ, â† union_compl_self { x | f x â‰¤ b }, image_union, bddAbove_union]
  exact âŸ¨âŸ¨b, forall_mem_image.2 fun x => idâŸ©, (hb.image f).bddAboveâŸ©

theorem IsBoundedUnder.bddBelow_range_of_cofinite [Preorder Î²] [IsDirected Î² (Â· â‰¥ Â·)] {f : Î± â†’ Î²}
    (hf : IsBoundedUnder (Â· â‰¥ Â·) cofinite f) : BddBelow (range f) :=
  IsBoundedUnder.bddAbove_range_of_cofinite (Î² := Î²áµ’áµˆ) hf

theorem IsBoundedUnder.bddAbove_range [Preorder Î²] [IsDirected Î² (Â· â‰¤ Â·)] {f : â„• â†’ Î²}
    (hf : IsBoundedUnder (Â· â‰¤ Â·) atTop f) : BddAbove (range f) := by
  rw [â† Nat.cofinite_eq_atTop] at hf
  exact hf.bddAbove_range_of_cofinite

theorem IsBoundedUnder.bddBelow_range [Preorder Î²] [IsDirected Î² (Â· â‰¥ Â·)] {f : â„• â†’ Î²}
    (hf : IsBoundedUnder (Â· â‰¥ Â·) atTop f) : BddBelow (range f) :=
  IsBoundedUnder.bddAbove_range (Î² := Î²áµ’áµˆ) hf

/-- To check that a filter is frequently bounded, it suffices to have a witness
which bounds `f` at some point for every admissible set.

This is only an implication, as the other direction is wrong for the trivial filter. -/
theorem IsCobounded.mk [IsTrans Î± r] (a : Î±) (h : âˆ€ s âˆˆ f, âˆƒ x âˆˆ s, r a x) : f.IsCobounded r :=
  âŸ¨a, fun _ s =>
    let âŸ¨_, hâ‚, hâ‚‚âŸ© := h _ s
    _root_.trans hâ‚‚ hâ‚âŸ©

/-- A filter which is eventually bounded is in particular frequently bounded (in the opposite
direction). At least if the filter is not trivial. -/
theorem IsBounded.isCobounded_flip [IsTrans Î± r] [NeBot f] : f.IsBounded r â†’ f.IsCobounded (flip r)
  | âŸ¨a, haâŸ© =>
    âŸ¨a, fun b hb =>
      let âŸ¨_, rxa, rbxâŸ© := (ha.and hb).exists
      show r b a from _root_.trans rbx rxaâŸ©

theorem IsBounded.isCobounded_ge [Preorder Î±] [NeBot f] (h : f.IsBounded (Â· â‰¤ Â·)) :
    f.IsCobounded (Â· â‰¥ Â·) :=
  h.isCobounded_flip

theorem IsBounded.isCobounded_le [Preorder Î±] [NeBot f] (h : f.IsBounded (Â· â‰¥ Â·)) :
    f.IsCobounded (Â· â‰¤ Â·) :=
  h.isCobounded_flip

theorem IsBoundedUnder.isCoboundedUnder_flip {u : Î³ â†’ Î±} {l : Filter Î³} [IsTrans Î± r] [NeBot l]
    (h : l.IsBoundedUnder r u) : l.IsCoboundedUnder (flip r) u :=
  h.isCobounded_flip

theorem IsBoundedUnder.isCoboundedUnder_le {u : Î³ â†’ Î±} {l : Filter Î³} [Preorder Î±] [NeBot l]
    (h : l.IsBoundedUnder (Â· â‰¥ Â·) u) : l.IsCoboundedUnder (Â· â‰¤ Â·) u :=
  h.isCoboundedUnder_flip

theorem IsBoundedUnder.isCoboundedUnder_ge {u : Î³ â†’ Î±} {l : Filter Î³} [Preorder Î±] [NeBot l]
    (h : l.IsBoundedUnder (Â· â‰¤ Â·) u) : l.IsCoboundedUnder (Â· â‰¥ Â·) u :=
  h.isCoboundedUnder_flip

lemma isCoboundedUnder_le_of_eventually_le [Preorder Î±] (l : Filter Î¹) [NeBot l] {f : Î¹ â†’ Î±} {x : Î±}
    (hf : âˆ€á¶  i in l, x â‰¤ f i) :
    IsCoboundedUnder (Â· â‰¤ Â·) l f :=
  IsBoundedUnder.isCoboundedUnder_le âŸ¨x, hfâŸ©

lemma isCoboundedUnder_ge_of_eventually_le [Preorder Î±] (l : Filter Î¹) [NeBot l] {f : Î¹ â†’ Î±} {x : Î±}
    (hf : âˆ€á¶  i in l, f i â‰¤ x) :
    IsCoboundedUnder (Â· â‰¥ Â·) l f :=
  IsBoundedUnder.isCoboundedUnder_ge âŸ¨x, hfâŸ©

lemma isCoboundedUnder_le_of_le [Preorder Î±] (l : Filter Î¹) [NeBot l] {f : Î¹ â†’ Î±} {x : Î±}
    (hf : âˆ€ i, x â‰¤ f i) :
    IsCoboundedUnder (Â· â‰¤ Â·) l f :=
  isCoboundedUnder_le_of_eventually_le l (Eventually.of_forall hf)

lemma isCoboundedUnder_ge_of_le [Preorder Î±] (l : Filter Î¹) [NeBot l] {f : Î¹ â†’ Î±} {x : Î±}
    (hf : âˆ€ i, f i â‰¤ x) :
    IsCoboundedUnder (Â· â‰¥ Â·) l f :=
  isCoboundedUnder_ge_of_eventually_le l (Eventually.of_forall hf)

theorem isCobounded_bot : IsCobounded r âŠ¥ â†” âˆƒ b, âˆ€ x, r b x := by simp [IsCobounded]

theorem isCobounded_top : IsCobounded r âŠ¤ â†” Nonempty Î± := by
  simp +contextual [IsCobounded,
    exists_true_iff_nonempty]

theorem isCobounded_principal (s : Set Î±) :
    (ğ“Ÿ s).IsCobounded r â†” âˆƒ b, âˆ€ a, (âˆ€ x âˆˆ s, r x a) â†’ r b a := by simp [IsCobounded]

theorem IsCobounded.mono (h : f â‰¤ g) : f.IsCobounded r â†’ g.IsCobounded r
  | âŸ¨b, hbâŸ© => âŸ¨b, fun a ha => hb a (h ha)âŸ©

/-- For nontrivial filters in linear orders, coboundedness for `â‰¤` implies frequent boundedness
from below. -/
lemma IsCobounded.frequently_ge [LinearOrder Î±] [NeBot f] (cobdd : IsCobounded (Â· â‰¤ Â·) f) :
    âˆƒ l, âˆƒá¶  x in f, l â‰¤ x := by
  obtain âŸ¨t, htâŸ© := cobdd
  rcases isBot_or_exists_lt t with tbot | âŸ¨t', ht'âŸ©
  Â· exact âŸ¨t, .of_forall fun r â†¦ tbot râŸ©
  refine âŸ¨t', fun ev â†¦ ?_âŸ©
  specialize ht t' (by filter_upwards [ev] with _ h using (not_le.mp h).le)
  exact not_lt_of_ge ht ht'

/-- For nontrivial filters in linear orders, coboundedness for `â‰¥` implies frequent boundedness
from above. -/
lemma IsCobounded.frequently_le [LinearOrder Î±] [NeBot f] (cobdd : IsCobounded (Â· â‰¥ Â·) f) :
    âˆƒ u, âˆƒá¶  x in f, x â‰¤ u :=
  cobdd.frequently_ge (Î± := Î±áµ’áµˆ)

/-- In linear orders, frequent boundedness from below implies coboundedness for `â‰¤`. -/
lemma IsCobounded.of_frequently_ge [LinearOrder Î±] {l : Î±} (freq_ge : âˆƒá¶  x in f, l â‰¤ x) :
    IsCobounded (Â· â‰¤ Â·) f := by
  rcases isBot_or_exists_lt l with lbot | âŸ¨l', hl'âŸ©
  Â· exact âŸ¨l, fun x _ â†¦ lbot xâŸ©
  refine âŸ¨l', fun u hu â†¦ ?_âŸ©
  obtain âŸ¨w, l_le_w, w_le_uâŸ© := (freq_ge.and_eventually hu).exists
  exact hl'.le.trans (l_le_w.trans w_le_u)

/-- In linear orders, frequent boundedness from above implies coboundedness for `â‰¥`. -/
lemma IsCobounded.of_frequently_le [LinearOrder Î±] {u : Î±} (freq_le : âˆƒá¶  r in f, r â‰¤ u) :
    IsCobounded (Â· â‰¥ Â·) f :=
  IsCobounded.of_frequently_ge (Î± := Î±áµ’áµˆ) freq_le

lemma IsCoboundedUnder.frequently_ge [LinearOrder Î±] {f : Filter Î¹} [NeBot f] {u : Î¹ â†’ Î±}
    (h : IsCoboundedUnder (Â· â‰¤ Â·) f u) :
    âˆƒ a, âˆƒá¶  x in f, a â‰¤ u x :=
  IsCobounded.frequently_ge h

lemma IsCoboundedUnder.frequently_le [LinearOrder Î±] {f : Filter Î¹} [NeBot f] {u : Î¹ â†’ Î±}
    (h : IsCoboundedUnder (Â· â‰¥ Â·) f u) :
    âˆƒ a, âˆƒá¶  x in f, u x â‰¤ a :=
  IsCobounded.frequently_le h

lemma IsCoboundedUnder.of_frequently_ge [LinearOrder Î±] {f : Filter Î¹} {u : Î¹ â†’ Î±}
    {a : Î±} (freq_ge : âˆƒá¶  x in f, a â‰¤ u x) :
    IsCoboundedUnder (Â· â‰¤ Â·) f u :=
  IsCobounded.of_frequently_ge freq_ge

lemma IsCoboundedUnder.of_frequently_le [LinearOrder Î±] {f : Filter Î¹} {u : Î¹ â†’ Î±}
    {a : Î±} (freq_le : âˆƒá¶  x in f, u x â‰¤ a) :
    IsCoboundedUnder (Â· â‰¥ Â·) f u :=
  IsCobounded.of_frequently_le freq_le

end Relation

section add_and_sum

open Filter Set

variable {Î± : Type*} {f : Filter Î±}
variable {R : Type*}

lemma isBoundedUnder_sum {Îº : Type*} [AddCommMonoid R] {r : R â†’ R â†’ Prop}
    (hr : âˆ€ (vâ‚ vâ‚‚ : Î± â†’ R), f.IsBoundedUnder r vâ‚ â†’ f.IsBoundedUnder r vâ‚‚
      â†’ f.IsBoundedUnder r (vâ‚ + vâ‚‚)) (hrâ‚€ : r 0 0)
    {u : Îº â†’ Î± â†’ R} (s : Finset Îº) (h : âˆ€ k âˆˆ s, f.IsBoundedUnder r (u k)) :
    f.IsBoundedUnder r (âˆ‘ k âˆˆ s, u k) := by
  induction s using Finset.cons_induction
  case empty =>
    rw [Finset.sum_empty]
    exact âŸ¨0, by simp_all only [eventually_map, Pi.zero_apply, eventually_true]âŸ©
  case cons kâ‚€ s kâ‚€_notin_s ih =>
    simp only [Finset.forall_mem_cons] at *
    simpa only [Finset.sum_cons] using hr _ _ h.1 (ih h.2)

variable [Preorder R]

lemma isBoundedUnder_ge_add [Add R] [AddLeftMono R] [AddRightMono R]
    {u v : Î± â†’ R} (u_bdd_ge : f.IsBoundedUnder (Â· â‰¥ Â·) u) (v_bdd_ge : f.IsBoundedUnder (Â· â‰¥ Â·) v) :
    f.IsBoundedUnder (Â· â‰¥ Â·) (u + v) := by
  obtain âŸ¨U, hUâŸ© := u_bdd_ge
  obtain âŸ¨V, hVâŸ© := v_bdd_ge
  use U + V
  simp only [eventually_map, Pi.add_apply] at hU hV âŠ¢
  filter_upwards [hU, hV] with a hu hv using add_le_add hu hv

lemma isBoundedUnder_le_add [Add R] [AddLeftMono R] [AddRightMono R]
    {u v : Î± â†’ R} (u_bdd_le : f.IsBoundedUnder (Â· â‰¤ Â·) u) (v_bdd_le : f.IsBoundedUnder (Â· â‰¤ Â·) v) :
    f.IsBoundedUnder (Â· â‰¤ Â·) (u + v) := by
  obtain âŸ¨U, hUâŸ© := u_bdd_le
  obtain âŸ¨V, hVâŸ© := v_bdd_le
  use U + V
  simp only [eventually_map, Pi.add_apply] at hU hV âŠ¢
  filter_upwards [hU, hV] with a hu hv using add_le_add hu hv

lemma isBoundedUnder_le_sum {Îº : Type*} [AddCommMonoid R] [AddLeftMono R] [AddRightMono R]
    {u : Îº â†’ Î± â†’ R} (s : Finset Îº) :
    (âˆ€ k âˆˆ s, f.IsBoundedUnder (Â· â‰¤ Â·) (u k)) â†’ f.IsBoundedUnder (Â· â‰¤ Â·) (âˆ‘ k âˆˆ s, u k) :=
  fun h â†¦ isBoundedUnder_sum (fun _ _ â†¦ isBoundedUnder_le_add) le_rfl s h

lemma isBoundedUnder_ge_sum {Îº : Type*} [AddCommMonoid R] [AddLeftMono R] [AddRightMono R]
    {u : Îº â†’ Î± â†’ R} (s : Finset Îº) :
    (âˆ€ k âˆˆ s, f.IsBoundedUnder (Â· â‰¥ Â·) (u k)) â†’
      f.IsBoundedUnder (Â· â‰¥ Â·) (âˆ‘ k âˆˆ s, u k) :=
  fun h â†¦ isBoundedUnder_sum (fun _ _ â†¦ isBoundedUnder_ge_add) le_rfl s h

end add_and_sum

section add_and_sum

variable {Î± : Type*} {R : Type*} [LinearOrder R] [Add R] {f : Filter Î±} [f.NeBot]
  [AddLeftMono R] [AddRightMono R]
  {u v : Î± â†’ R}

lemma isCoboundedUnder_ge_add (hu : f.IsBoundedUnder (Â· â‰¤ Â·) u)
    (hv : f.IsCoboundedUnder (Â· â‰¥ Â·) v) :
    f.IsCoboundedUnder (Â· â‰¥ Â·) (u + v) := by
  obtain âŸ¨U, hUâŸ© := hu.eventually_le
  obtain âŸ¨V, hVâŸ© := hv.frequently_le
  apply IsCoboundedUnder.of_frequently_le (a := U + V)
  exact (hV.and_eventually hU).mono fun x hx â†¦ add_le_add hx.2 hx.1

lemma isCoboundedUnder_le_add (hu : f.IsBoundedUnder (Â· â‰¥ Â·) u)
    (hv : f.IsCoboundedUnder (Â· â‰¤ Â·) v) :
    f.IsCoboundedUnder (Â· â‰¤ Â·) (u + v) := by
  obtain âŸ¨U, hUâŸ© := hu.eventually_ge
  obtain âŸ¨V, hVâŸ© := hv.frequently_ge
  apply IsCoboundedUnder.of_frequently_ge (a := U + V)
  exact (hV.and_eventually hU).mono fun x hx â†¦ add_le_add hx.2 hx.1

end add_and_sum

section mul

lemma isBoundedUnder_le_mul_of_nonneg [Preorder Î±] [Mul Î±] [Zero Î±] [PosMulMono Î±]
    [MulPosMono Î±] {f : Filter Î¹} {u v : Î¹ â†’ Î±} (hâ‚ : âˆƒá¶  x in f, 0 â‰¤ u x)
    (hâ‚‚ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¤ x2) f u) (hâ‚ƒ : 0 â‰¤á¶ [f] v)
    (hâ‚„ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¤ x2) f v) :
    IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¤ x2) f (u * v) := by
  obtain âŸ¨U, hUâŸ© := hâ‚‚.eventually_le
  obtain âŸ¨V, hVâŸ© := hâ‚„.eventually_le
  refine isBoundedUnder_of_eventually_le (a := U * V) ?_
  filter_upwards [hU, hV, hâ‚ƒ] with x x_U x_V v_0
  have U_0 : 0 â‰¤ U := by
    obtain âŸ¨y, y_0, y_UâŸ© := (hâ‚.and_eventually hU).exists
    exact y_0.trans y_U
  exact (mul_le_mul_of_nonneg_right x_U v_0).trans (mul_le_mul_of_nonneg_left x_V U_0)

lemma isCoboundedUnder_ge_mul_of_nonneg [LinearOrder Î±] [Mul Î±] [Zero Î±] [PosMulMono Î±]
    [MulPosMono Î±] {f : Filter Î¹} [f.NeBot] {u v : Î¹ â†’ Î±} (hâ‚ : 0 â‰¤á¶ [f] u)
    (hâ‚‚ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¤ x2) f u)
    (hâ‚ƒ : 0 â‰¤á¶ [f] v)
    (hâ‚„ : IsCoboundedUnder (fun x1 x2 â†¦ x1 â‰¥ x2) f v) :
    IsCoboundedUnder (fun x1 x2 â†¦ x1 â‰¥ x2) f (u * v) := by
  obtain âŸ¨U, hUâŸ© := hâ‚‚.eventually_le
  obtain âŸ¨V, hVâŸ© := hâ‚„.frequently_le
  refine IsCoboundedUnder.of_frequently_le (a := U * V) ?_
  apply (hV.and_eventually (hU.and (hâ‚.and hâ‚ƒ))).mono
  intro x âŸ¨x_V, x_U, u_0, v_0âŸ©
  exact (mul_le_mul_of_nonneg_right x_U v_0).trans (mul_le_mul_of_nonneg_left x_V (u_0.trans x_U))

end mul

section Nonempty
variable [Preorder Î±] [Nonempty Î±] {f : Filter Î²} {u : Î² â†’ Î±}

theorem isBounded_le_atBot : (atBot : Filter Î±).IsBounded (Â· â‰¤ Â·) :=
  â€¹Nonempty Î±â€º.elim fun a => âŸ¨a, eventually_le_atBot _âŸ©

theorem isBounded_ge_atTop : (atTop : Filter Î±).IsBounded (Â· â‰¥ Â·) :=
  â€¹Nonempty Î±â€º.elim fun a => âŸ¨a, eventually_ge_atTop _âŸ©

theorem Tendsto.isBoundedUnder_le_atBot (h : Tendsto u f atBot) : f.IsBoundedUnder (Â· â‰¤ Â·) u :=
  isBounded_le_atBot.mono h

theorem Tendsto.isBoundedUnder_ge_atTop (h : Tendsto u f atTop) : f.IsBoundedUnder (Â· â‰¥ Â·) u :=
  isBounded_ge_atTop.mono h

theorem bddAbove_range_of_tendsto_atTop_atBot [IsDirected Î± (Â· â‰¤ Â·)] {u : â„• â†’ Î±}
    (hx : Tendsto u atTop atBot) : BddAbove (Set.range u) :=
  hx.isBoundedUnder_le_atBot.bddAbove_range

theorem bddBelow_range_of_tendsto_atTop_atTop [IsDirected Î± (Â· â‰¥ Â·)] {u : â„• â†’ Î±}
    (hx : Tendsto u atTop atTop) : BddBelow (Set.range u) :=
  hx.isBoundedUnder_ge_atTop.bddBelow_range

end Nonempty

theorem isCobounded_le_of_bot [LE Î±] [OrderBot Î±] {f : Filter Î±} : f.IsCobounded (Â· â‰¤ Â·) :=
  âŸ¨âŠ¥, fun _ _ => bot_leâŸ©

theorem isCobounded_ge_of_top [LE Î±] [OrderTop Î±] {f : Filter Î±} : f.IsCobounded (Â· â‰¥ Â·) :=
  âŸ¨âŠ¤, fun _ _ => le_topâŸ©

theorem isBounded_le_of_top [LE Î±] [OrderTop Î±] {f : Filter Î±} : f.IsBounded (Â· â‰¤ Â·) :=
  âŸ¨âŠ¤, Eventually.of_forall fun _ => le_topâŸ©

theorem isBounded_ge_of_bot [LE Î±] [OrderBot Î±] {f : Filter Î±} : f.IsBounded (Â· â‰¥ Â·) :=
  âŸ¨âŠ¥, Eventually.of_forall fun _ => bot_leâŸ©

@[simp]
theorem _root_.OrderIso.isBoundedUnder_le_comp [LE Î±] [LE Î²] (e : Î± â‰ƒo Î²) {l : Filter Î³}
    {u : Î³ â†’ Î±} : (IsBoundedUnder (Â· â‰¤ Â·) l fun x => e (u x)) â†” IsBoundedUnder (Â· â‰¤ Â·) l u :=
  (Function.Surjective.exists e.surjective).trans <|
    exists_congr fun a => by simp only [eventually_map, e.le_iff_le]

@[simp]
theorem _root_.OrderIso.isBoundedUnder_ge_comp [LE Î±] [LE Î²] (e : Î± â‰ƒo Î²) {l : Filter Î³}
    {u : Î³ â†’ Î±} : (IsBoundedUnder (Â· â‰¥ Â·) l fun x => e (u x)) â†” IsBoundedUnder (Â· â‰¥ Â·) l u :=
  OrderIso.isBoundedUnder_le_comp e.dual

@[to_additive (attr := simp)]
theorem isBoundedUnder_le_inv [CommGroup Î±] [PartialOrder Î±] [IsOrderedMonoid Î±]
    {l : Filter Î²} {u : Î² â†’ Î±} :
    (IsBoundedUnder (Â· â‰¤ Â·) l fun x => (u x)â»Â¹) â†” IsBoundedUnder (Â· â‰¥ Â·) l u :=
  (OrderIso.inv Î±).isBoundedUnder_ge_comp

@[to_additive (attr := simp)]
theorem isBoundedUnder_ge_inv [CommGroup Î±] [PartialOrder Î±] [IsOrderedMonoid Î±]
    {l : Filter Î²} {u : Î² â†’ Î±} :
    (IsBoundedUnder (Â· â‰¥ Â·) l fun x => (u x)â»Â¹) â†” IsBoundedUnder (Â· â‰¤ Â·) l u :=
  (OrderIso.inv Î±).isBoundedUnder_le_comp

theorem IsBoundedUnder.sup [SemilatticeSup Î±] {f : Filter Î²} {u v : Î² â†’ Î±} :
    f.IsBoundedUnder (Â· â‰¤ Â·) u â†’
      f.IsBoundedUnder (Â· â‰¤ Â·) v â†’ f.IsBoundedUnder (Â· â‰¤ Â·) fun a => u a âŠ” v a
  | âŸ¨bu, (hu : âˆ€á¶  x in f, u x â‰¤ bu)âŸ©, âŸ¨bv, (hv : âˆ€á¶  x in f, v x â‰¤ bv)âŸ© =>
    âŸ¨bu âŠ” bv, show âˆ€á¶  x in f, u x âŠ” v x â‰¤ bu âŠ” bv
      by filter_upwards [hu, hv] with _ using sup_le_supâŸ©

@[simp]
theorem isBoundedUnder_le_sup [SemilatticeSup Î±] {f : Filter Î²} {u v : Î² â†’ Î±} :
    (f.IsBoundedUnder (Â· â‰¤ Â·) fun a => u a âŠ” v a) â†”
      f.IsBoundedUnder (Â· â‰¤ Â·) u âˆ§ f.IsBoundedUnder (Â· â‰¤ Â·) v :=
  âŸ¨fun h =>
    âŸ¨h.mono_le <| Eventually.of_forall fun _ => le_sup_left,
      h.mono_le <| Eventually.of_forall fun _ => le_sup_rightâŸ©,
    fun h => h.1.sup h.2âŸ©

theorem IsBoundedUnder.inf [SemilatticeInf Î±] {f : Filter Î²} {u v : Î² â†’ Î±} :
    f.IsBoundedUnder (Â· â‰¥ Â·) u â†’
      f.IsBoundedUnder (Â· â‰¥ Â·) v â†’ f.IsBoundedUnder (Â· â‰¥ Â·) fun a => u a âŠ“ v a :=
  IsBoundedUnder.sup (Î± := Î±áµ’áµˆ)

@[simp]
theorem isBoundedUnder_ge_inf [SemilatticeInf Î±] {f : Filter Î²} {u v : Î² â†’ Î±} :
    (f.IsBoundedUnder (Â· â‰¥ Â·) fun a => u a âŠ“ v a) â†”
      f.IsBoundedUnder (Â· â‰¥ Â·) u âˆ§ f.IsBoundedUnder (Â· â‰¥ Â·) v :=
  isBoundedUnder_le_sup (Î± := Î±áµ’áµˆ)

theorem isBoundedUnder_le_abs [AddCommGroup Î±] [LinearOrder Î±] [IsOrderedAddMonoid Î±]
    {f : Filter Î²} {u : Î² â†’ Î±} :
    (f.IsBoundedUnder (Â· â‰¤ Â·) fun a => |u a|) â†”
      f.IsBoundedUnder (Â· â‰¤ Â·) u âˆ§ f.IsBoundedUnder (Â· â‰¥ Â·) u :=
  isBoundedUnder_le_sup.trans <| and_congr Iff.rfl isBoundedUnder_le_neg

/-- Filters are automatically bounded or cobounded in complete lattices. To use the same statements
in complete and conditionally complete lattices but let automation fill automatically the
boundedness proofs in complete lattices, we use the tactic `isBoundedDefault` in the statements,
in the form `(hf : f.IsBounded (â‰¥) := by isBoundedDefault)`. -/
macro "isBoundedDefault" : tactic =>
  `(tactic| first
    | apply isCobounded_le_of_bot
    | apply isCobounded_ge_of_top
    | apply isBounded_le_of_top
    | apply isBounded_ge_of_bot
    | assumption)

end Filter

open Filter

section Order

theorem Monotone.isBoundedUnder_le_comp_iff [Nonempty Î²] [LinearOrder Î²] [Preorder Î³] [NoMaxOrder Î³]
    {g : Î² â†’ Î³} {f : Î± â†’ Î²} {l : Filter Î±} (hg : Monotone g) (hg' : Tendsto g atTop atTop) :
    IsBoundedUnder (Â· â‰¤ Â·) l (g âˆ˜ f) â†” IsBoundedUnder (Â· â‰¤ Â·) l f := by
  refine âŸ¨?_, fun h => h.isBoundedUnder (Î± := Î²) hgâŸ©
  rintro âŸ¨c, hcâŸ©; rw [eventually_map] at hc
  obtain âŸ¨b, hbâŸ© : âˆƒ b, âˆ€ a â‰¥ b, c < g a := eventually_atTop.1 (hg'.eventually_gt_atTop c)
  exact âŸ¨b, hc.mono fun x hx => not_lt.1 fun h => (hb _ h.le).not_ge hxâŸ©

theorem Monotone.isBoundedUnder_ge_comp_iff [Nonempty Î²] [LinearOrder Î²] [Preorder Î³] [NoMinOrder Î³]
    {g : Î² â†’ Î³} {f : Î± â†’ Î²} {l : Filter Î±} (hg : Monotone g) (hg' : Tendsto g atBot atBot) :
    IsBoundedUnder (Â· â‰¥ Â·) l (g âˆ˜ f) â†” IsBoundedUnder (Â· â‰¥ Â·) l f :=
  hg.dual.isBoundedUnder_le_comp_iff hg'

theorem Antitone.isBoundedUnder_le_comp_iff [Nonempty Î²] [LinearOrder Î²] [Preorder Î³] [NoMaxOrder Î³]
    {g : Î² â†’ Î³} {f : Î± â†’ Î²} {l : Filter Î±} (hg : Antitone g) (hg' : Tendsto g atBot atTop) :
    IsBoundedUnder (Â· â‰¤ Â·) l (g âˆ˜ f) â†” IsBoundedUnder (Â· â‰¥ Â·) l f :=
  hg.dual_right.isBoundedUnder_ge_comp_iff hg'

theorem Antitone.isBoundedUnder_ge_comp_iff [Nonempty Î²] [LinearOrder Î²] [Preorder Î³] [NoMinOrder Î³]
    {g : Î² â†’ Î³} {f : Î± â†’ Î²} {l : Filter Î±} (hg : Antitone g) (hg' : Tendsto g atTop atBot) :
    IsBoundedUnder (Â· â‰¥ Â·) l (g âˆ˜ f) â†” IsBoundedUnder (Â· â‰¤ Â·) l f :=
  hg.dual_right.isBoundedUnder_le_comp_iff hg'

end Order

section MinMax

theorem isCoboundedUnder_le_max [LinearOrder Î²] {f : Filter Î±} {u v : Î± â†’ Î²}
    (h : f.IsCoboundedUnder (Â· â‰¤ Â·) u âˆ¨ f.IsCoboundedUnder (Â· â‰¤ Â·) v) :
    f.IsCoboundedUnder (Â· â‰¤ Â·) (fun a â†¦ max (u a) (v a)) := by
  rcases h with (h' | h') <;>
  Â· rcases h' with âŸ¨b, hbâŸ©
    use b
    intro c hc
    apply hb c
    rw [eventually_map] at hc âŠ¢
    refine hc.mono (fun _ â†¦ ?_)
    simp +contextual only [implies_true, max_le_iff]

open Finset

theorem isBoundedUnder_le_finset_sup' [LinearOrder Î²] [Nonempty Î²] {f : Filter Î±} {F : Î¹ â†’ Î± â†’ Î²}
    {s : Finset Î¹} (hs : s.Nonempty) (h : âˆ€ i âˆˆ s, f.IsBoundedUnder (Â· â‰¤ Â·) (F i)) :
    f.IsBoundedUnder (Â· â‰¤ Â·) (fun a â†¦ sup' s hs (fun i â†¦ F i a)) := by
  choose! m hm using h
  use sup' s hs m
  simp only [eventually_map] at hm âŠ¢
  rw [â† eventually_all_finset s] at hm
  refine hm.mono fun a h â†¦ ?_
  simp only [sup'_le_iff]
  exact fun i i_s â†¦ le_trans (h i i_s) (le_sup' m i_s)

theorem isCoboundedUnder_le_finset_sup' [LinearOrder Î²] {f : Filter Î±} {F : Î¹ â†’ Î± â†’ Î²}
    {s : Finset Î¹} (hs : s.Nonempty) (h : âˆƒ i âˆˆ s, f.IsCoboundedUnder (Â· â‰¤ Â·) (F i)) :
    f.IsCoboundedUnder (Â· â‰¤ Â·) (fun a â†¦ sup' s hs (fun i â†¦ F i a)) := by
  rcases h with âŸ¨i, i_s, b, hbâŸ©
  use b
  refine fun c hc â†¦ hb c ?_
  rw [eventually_map] at hc âŠ¢
  refine hc.mono fun a h â†¦ ?_
  simp only [sup'_le_iff] at h âŠ¢
  exact h i i_s

theorem isBoundedUnder_le_finset_sup [LinearOrder Î²] [OrderBot Î²] {f : Filter Î±} {F : Î¹ â†’ Î± â†’ Î²}
    {s : Finset Î¹} (h : âˆ€ i âˆˆ s, f.IsBoundedUnder (Â· â‰¤ Â·) (F i)) :
    f.IsBoundedUnder (Â· â‰¤ Â·) (fun a â†¦ sup s (fun i â†¦ F i a)) := by
  choose! m hm using h
  use sup s m
  simp only [eventually_map] at hm âŠ¢
  rw [â† eventually_all_finset s] at hm
  exact hm.mono fun _ h â†¦ sup_mono_fun h

theorem isBoundedUnder_ge_finset_inf' [LinearOrder Î²] [Nonempty Î²] {f : Filter Î±} {F : Î¹ â†’ Î± â†’ Î²}
    {s : Finset Î¹} (hs : s.Nonempty) (h : âˆ€ i âˆˆ s, f.IsBoundedUnder (Â· â‰¥ Â·) (F i)) :
    f.IsBoundedUnder (Â· â‰¥ Â·) (fun a â†¦ inf' s hs (fun i â†¦ F i a)) :=
  isBoundedUnder_le_finset_sup' (Î² := Î²áµ’áµˆ) hs h

theorem isCoboundedUnder_ge_finset_inf' [LinearOrder Î²] {f : Filter Î±} {F : Î¹ â†’ Î± â†’ Î²}
    {s : Finset Î¹} (hs : s.Nonempty) (h : âˆƒ i âˆˆ s, f.IsCoboundedUnder (Â· â‰¥ Â·) (F i)) :
    f.IsCoboundedUnder (Â· â‰¥ Â·) (fun a â†¦ inf' s hs (fun i â†¦ F i a)) :=
  isCoboundedUnder_le_finset_sup' (Î² := Î²áµ’áµˆ) hs h

theorem isBoundedUnder_ge_finset_inf [LinearOrder Î²] [OrderTop Î²] {f : Filter Î±} {F : Î¹ â†’ Î± â†’ Î²}
    {s : Finset Î¹} (h : âˆ€ i âˆˆ s, f.IsBoundedUnder (Â· â‰¥ Â·) (F i)) :
    f.IsBoundedUnder (Â· â‰¥ Â·) (fun a â†¦ inf s (fun i â†¦ F i a)) :=
  isBoundedUnder_le_finset_sup (Î² := Î²áµ’áµˆ) h

end MinMax

section FrequentlyBounded

variable {R S : Type*} {F : Filter R} [LinearOrder R] [LinearOrder S]

lemma Monotone.frequently_ge_map_of_frequently_ge {f : R â†’ S} (f_incr : Monotone f)
    {l : R} (freq_ge : âˆƒá¶  x in F, l â‰¤ x) :
    âˆƒá¶  x' in F.map f, f l â‰¤ x' := by
  refine fun ev â†¦ freq_ge ?_
  simp only [not_le] at ev freq_ge âŠ¢
  filter_upwards [ev] with z hz
  by_contra con
  exact lt_irrefl (f l) <| lt_of_le_of_lt (f_incr <| not_lt.mp con) hz

lemma Monotone.frequently_le_map_of_frequently_le {f : R â†’ S} (f_incr : Monotone f)
    {u : R} (freq_le : âˆƒá¶  x in F, x â‰¤ u) :
    âˆƒá¶  y in F.map f, y â‰¤ f u := by
  refine fun ev â†¦ freq_le ?_
  simp only [not_le] at ev freq_le âŠ¢
  filter_upwards [ev] with z hz
  by_contra con
  apply lt_irrefl (f u) <| lt_of_lt_of_le hz <| f_incr (not_lt.mp con)

lemma Antitone.frequently_le_map_of_frequently_ge {f : R â†’ S} (f_decr : Antitone f)
    {l : R} (frbdd : âˆƒá¶  x in F, l â‰¤ x) :
    âˆƒá¶  y in F.map f, y â‰¤ f l :=
  Monotone.frequently_ge_map_of_frequently_ge (S := Sáµ’áµˆ) f_decr frbdd

lemma Antitone.frequently_ge_map_of_frequently_le {f : R â†’ S} (f_decr : Antitone f)
    {u : R} (frbdd : âˆƒá¶  x in F, x â‰¤ u) :
    âˆƒá¶  y in F.map f, f u â‰¤ y :=
  Monotone.frequently_le_map_of_frequently_le (S := Sáµ’áµˆ) f_decr frbdd

lemma Monotone.isCoboundedUnder_le_of_isCobounded {f : R â†’ S} (f_incr : Monotone f)
    [NeBot F] (cobdd : IsCobounded (Â· â‰¤ Â·) F) :
    F.IsCoboundedUnder (Â· â‰¤ Â·) f := by
  obtain âŸ¨l, hlâŸ© := IsCobounded.frequently_ge cobdd
  exact IsCobounded.of_frequently_ge <| f_incr.frequently_ge_map_of_frequently_ge hl

lemma Monotone.isCoboundedUnder_ge_of_isCobounded {f : R â†’ S} (f_incr : Monotone f)
    [NeBot F] (cobdd : IsCobounded (Â· â‰¥ Â·) F) :
    F.IsCoboundedUnder (Â· â‰¥ Â·) f :=
  Monotone.isCoboundedUnder_le_of_isCobounded (R := Ráµ’áµˆ) (S := Sáµ’áµˆ) f_incr.dual cobdd

lemma Antitone.isCoboundedUnder_le_of_isCobounded {f : R â†’ S} (f_decr : Antitone f)
    [NeBot F] (cobdd : IsCobounded (Â· â‰¥ Â·) F) :
    F.IsCoboundedUnder (Â· â‰¤ Â·) f :=
  Monotone.isCoboundedUnder_le_of_isCobounded (R := Ráµ’áµˆ) f_decr.dual cobdd

lemma Antitone.isCoboundedUnder_ge_of_isCobounded {f : R â†’ S} (f_decr : Antitone f)
    [NeBot F] (cobdd : IsCobounded (Â· â‰¤ Â·) F) :
    F.IsCoboundedUnder (Â· â‰¥ Â·) f :=
  Monotone.isCoboundedUnder_le_of_isCobounded (S := Sáµ’áµˆ) f_decr cobdd

end FrequentlyBounded
