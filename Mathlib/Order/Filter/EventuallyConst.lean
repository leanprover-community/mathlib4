/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Floris van Doorn
-/
import Mathlib.Order.Filter.AtTopBot
import Mathlib.Order.Filter.Subsingleton
import Mathlib.Algebra.IndicatorFunction
/-!
# Functions that are eventually constant along a filter

In this file we define a predicate `Filter.EventuallyConst f l` saying that a function `f : Î± â†’ Î²`
is eventually equal to a constant along a filter `l`. We also prove some basic properties of these
functions.

## Implementation notes

A naive definition of `Filter.EventuallyConst f l` is `âˆƒ y, âˆ€á¶  x in l, f x = y`.
However, this proposition is false for empty `Î±`, `Î²`.
Instead, we say that `Filter.map f l` is supported on a subsingleton.
This allows us to drop `[Nonempty _]` assumptions here and there.
-/

open Set

variable {Î± Î² Î³ Î´ : Type*} {l : Filter Î±} {f : Î± â†’ Î²}

namespace Filter

/-- The proposition that a function is eventually constant along a filter on the domain. -/
def EventuallyConst (f : Î± â†’ Î²) (l : Filter Î±) : Prop := (map f l).Subsingleton

theorem HasBasis.eventuallyConst_iff {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±}
    (h : l.HasBasis p s) : EventuallyConst f l â†” âˆƒ i, p i âˆ§ âˆ€ x âˆˆ s i, âˆ€ y âˆˆ s i, f x = f y :=
  (h.map f).subsingleton_iff.trans <| by simp only [Set.Subsingleton, ball_image_iff]
                                         -- ðŸŽ‰ no goals

theorem HasBasis.eventuallyConst_iff' {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±}
    {x : Î¹ â†’ Î±} (h : l.HasBasis p s) (hx : âˆ€ i, p i â†’ x i âˆˆ s i) :
    EventuallyConst f l â†” âˆƒ i, p i âˆ§ âˆ€ y âˆˆ s i, f y = f (x i) :=
  h.eventuallyConst_iff.trans <| exists_congr fun i â†¦ and_congr_right fun hi â†¦
    âŸ¨fun h â†¦ (h Â· Â· (x i) (hx i hi)), fun h a ha b hb â†¦ h a ha â–¸ (h b hb).symmâŸ©

lemma eventuallyConst_iff_tendsto [Nonempty Î²] :
    EventuallyConst f l â†” âˆƒ x, Tendsto f l (pure x) :=
  subsingleton_iff_exists_le_pure

alias âŸ¨EventuallyConst.exists_tendsto, _âŸ© := eventuallyConst_iff_tendsto

theorem EventuallyConst.of_tendsto {x : Î²} (h : Tendsto f l (pure x)) : EventuallyConst f l :=
  have : Nonempty Î² := âŸ¨xâŸ©; eventuallyConst_iff_tendsto.2 âŸ¨x, hâŸ©

theorem eventuallyConst_iff_exists_eventuallyEq [Nonempty Î²] :
    EventuallyConst f l â†” âˆƒ c, f =á¶ [l] fun _ â†¦ c :=
  subsingleton_iff_exists_singleton_mem

alias âŸ¨EventuallyConst.eventuallyEq_const, _âŸ© := eventuallyConst_iff_exists_eventuallyEq

theorem eventuallyConst_pred' {p : Î± â†’ Prop} :
    EventuallyConst p l â†” (p =á¶ [l] fun _ â†¦ False) âˆ¨ (p =á¶ [l] fun _ â†¦ True) := by
  simp only [eventuallyConst_iff_exists_eventuallyEq, Prop.exists_iff]
  -- ðŸŽ‰ no goals

theorem eventuallyConst_pred {p : Î± â†’ Prop} :
    EventuallyConst p l â†” (âˆ€á¶  x in l, p x) âˆ¨ (âˆ€á¶  x in l, Â¬p x) := by
  simp [eventuallyConst_pred', or_comm, EventuallyEq]
  -- ðŸŽ‰ no goals

theorem eventuallyConst_set' {s : Set Î±} :
    EventuallyConst s l â†” (s =á¶ [l] (âˆ… : Set Î±)) âˆ¨ s =á¶ [l] univ :=
  eventuallyConst_pred'

theorem eventuallyConst_set {s : Set Î±} :
    EventuallyConst s l â†” (âˆ€á¶  x in l, x âˆˆ s) âˆ¨ (âˆ€á¶  x in l, x âˆ‰ s) :=
  eventuallyConst_pred

theorem EventuallyEq.eventuallyConst_iff {g : Î± â†’ Î²} (h : f =á¶ [l] g) :
    EventuallyConst f l â†” EventuallyConst g l := by
  simp only [EventuallyConst, map_congr h]
  -- ðŸŽ‰ no goals

@[simp] theorem eventuallyConst_id : EventuallyConst id l â†” l.Subsingleton := Iff.rfl

namespace EventuallyConst

@[simp] protected lemma bot : EventuallyConst f âŠ¥ := subsingleton_bot

@[simp]
protected lemma const (c : Î²) : EventuallyConst (fun _ â†¦ c) l :=
  .of_tendsto tendsto_const_pure

protected lemma congr {g} (h : EventuallyConst f l) (hg : f =á¶ [l] g) : EventuallyConst g l :=
  hg.eventuallyConst_iff.1 h

@[nontriviality]
lemma of_subsingleton_right [Subsingleton Î²] : EventuallyConst f l := .of_subsingleton

nonrec lemma anti {l'} (h : EventuallyConst f l) (hl' : l' â‰¤ l) : EventuallyConst f l' :=
  h.anti (map_mono hl')

@[nontriviality]
lemma of_subsingleton_left [Subsingleton Î±] : EventuallyConst f l :=
  .map .of_subsingleton f

lemma comp (h : EventuallyConst f l) (g : Î² â†’ Î³) : EventuallyConst (g âˆ˜ f) l := h.map g

@[to_additive]
protected lemma inv [Inv Î²] (h : EventuallyConst f l) : EventuallyConst (fâ»Â¹) l := h.comp Inv.inv

lemma comp_tendsto {lb : Filter Î²} {g : Î² â†’ Î³} (hg : EventuallyConst g lb)
    (hf : Tendsto f l lb) : EventuallyConst (g âˆ˜ f) l :=
  hg.anti hf

lemma apply {Î¹ : Type*} {p : Î¹ â†’ Type*} {g : Î± â†’ âˆ€ x, p x}
    (h : EventuallyConst g l) (i : Î¹) : EventuallyConst (g Â· i) l :=
  h.comp <| Function.eval i

lemma compâ‚‚ {g : Î± â†’ Î³} (hf : EventuallyConst f l) (op : Î² â†’ Î³ â†’ Î´) (hg : EventuallyConst g l) :
    EventuallyConst (fun x â†¦ op (f x) (g x)) l :=
  ((hf.prod hg).map op.uncurry).anti <|
    (tendsto_map (f := op.uncurry)).comp (tendsto_map.prod_mk tendsto_map)

lemma prod_mk {g : Î± â†’ Î³} (hf : EventuallyConst f l) (hg : EventuallyConst g l) :
    EventuallyConst (fun x â†¦ (f x, g x)) l :=
  hf.compâ‚‚ Prod.mk hg

@[to_additive]
lemma mul [Mul Î²] {g : Î± â†’ Î²} (hf : EventuallyConst f l) (hg : EventuallyConst g l) :
    EventuallyConst (f * g) l :=
  hf.compâ‚‚ (Â· * Â·) hg

variable [One Î²] {s : Set Î±} {c : Î²}

@[to_additive]
lemma of_mulIndicator_const (h : EventuallyConst (s.mulIndicator fun _ â†¦ c) l) (hc : c â‰  1) :
    EventuallyConst s l := by
  simpa [(Â· âˆ˜ Â·), hc, imp_false] using h.comp (Â· = c)
  -- ðŸŽ‰ no goals

@[to_additive]
theorem mulIndicator_const (h : EventuallyConst s l) (c : Î²) :
    EventuallyConst (s.mulIndicator fun _ â†¦ c) l := by
  classical exact h.comp (if Â· then c else 1)
  -- ðŸŽ‰ no goals

@[to_additive]
theorem mulIndicator_const_iff_of_ne (hc : c â‰  1) :
    EventuallyConst (s.mulIndicator fun _ â†¦ c) l â†” EventuallyConst s l :=
  âŸ¨(of_mulIndicator_const Â· hc), (mulIndicator_const Â· c)âŸ©

@[to_additive (attr := simp)]
theorem mulIndicator_const_iff :
    EventuallyConst (s.mulIndicator fun _ â†¦ c) l â†” c = 1 âˆ¨ EventuallyConst s l := by
  rcases eq_or_ne c 1 with rfl | hc <;> simp [mulIndicator_const_iff_of_ne, *]
  -- âŠ¢ EventuallyConst (mulIndicator s fun x => 1) l â†” 1 = 1 âˆ¨ EventuallyConst s l
                                        -- ðŸŽ‰ no goals
                                        -- ðŸŽ‰ no goals

end EventuallyConst

lemma eventuallyConst_atTop [SemilatticeSup Î±] [Nonempty Î±] :
    EventuallyConst f atTop â†” (âˆƒ i, âˆ€ j, i â‰¤ j â†’ f j = f i) :=
  (atTop_basis.eventuallyConst_iff' fun i _ â†¦ left_mem_Ici).trans <| by
    simp only [true_and, mem_Ici]
    -- ðŸŽ‰ no goals

lemma eventuallyConst_atTop_nat {f : â„• â†’ Î±} :
    EventuallyConst f atTop â†” âˆƒ n, âˆ€ m, n â‰¤ m â†’ f (m + 1) = f m := by
  rw [eventuallyConst_atTop]
  -- âŠ¢ (âˆƒ i, âˆ€ (j : â„•), i â‰¤ j â†’ f j = f i) â†” âˆƒ n, âˆ€ (m : â„•), n â‰¤ m â†’ f (m + 1) = f m
  refine exists_congr fun n â†¦ âŸ¨fun h m hm â†¦ ?_, fun h m hm â†¦ ?_âŸ©
  -- âŠ¢ f (m + 1) = f m
  Â· exact (h (m + 1) (hm.trans m.le_succ)).trans (h m hm).symm
    -- ðŸŽ‰ no goals
  Â· induction m, hm using Nat.le_induction with
    | base => rfl
    | succ m hm ihm => exact (h m hm).trans ihm
