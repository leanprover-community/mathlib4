/-
Copyright (c) 2022 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Floris van Doorn, Yury Kudryashov
-/
import Mathlib.Order.Filter.Lift
import Mathlib.Order.Filter.AtTopBot

#align_import order.filter.small_sets from "leanprover-community/mathlib"@"8631e2d5ea77f6c13054d9151d82b83069680cb1"

/-!
# The filter of small sets

This file defines the filter of small sets w.r.t. a filter `f`, which is the largest filter
containing all powersets of members of `f`.

`g` converges to `f.smallSets` if for all `s âˆˆ f`, eventually we have `g x âŠ† s`.

An example usage is that if `f : Î¹ â†’ E â†’ â„` is a family of nonnegative functions with integral 1,
then saying that `fun i â†¦ support (f i)` tendsto `(ğ“ 0).smallSets` is a way of saying that
`f` tends to the Dirac delta distribution.
-/


open Filter

open Filter Set

variable {Î± Î² : Type*} {Î¹ : Sort*}

namespace Filter

variable {l l' la : Filter Î±} {lb : Filter Î²}

/-- The filter `l.smallSets` is the largest filter containing all powersets of members of `l`. -/
def smallSets (l : Filter Î±) : Filter (Set Î±) :=
  l.lift' powerset
#align filter.small_sets Filter.smallSets

theorem smallSets_eq_generate {f : Filter Î±} : f.smallSets = generate (powerset '' f.sets) := by
  simp_rw [generate_eq_biInf, smallSets, iInf_image]
  rfl
#align filter.small_sets_eq_generate Filter.smallSets_eq_generate

-- TODO: get more properties from the adjunction?
-- TODO: is there a general way to get a lower adjoint for the lift of an upper adjoint?
theorem bind_smallSets_gc :
    GaloisConnection (fun L : Filter (Set Î±) â†¦ L.bind principal) smallSets := by
  intro L l
  simp_rw [smallSets_eq_generate, le_generate_iff, image_subset_iff]
  rfl

protected theorem HasBasis.smallSets {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±} (h : HasBasis l p s) :
    HasBasis l.smallSets p fun i => ğ’« s i :=
  h.lift' monotone_powerset
#align filter.has_basis.small_sets Filter.HasBasis.smallSets

theorem hasBasis_smallSets (l : Filter Î±) :
    HasBasis l.smallSets (fun t : Set Î± => t âˆˆ l) powerset :=
  l.basis_sets.smallSets
#align filter.has_basis_small_sets Filter.hasBasis_smallSets

/-- `g` converges to `f.smallSets` if for all `s âˆˆ f`, eventually we have `g x âŠ† s`. -/
theorem tendsto_smallSets_iff {f : Î± â†’ Set Î²} :
    Tendsto f la lb.smallSets â†” âˆ€ t âˆˆ lb, âˆ€á¶  x in la, f x âŠ† t :=
  (hasBasis_smallSets lb).tendsto_right_iff
#align filter.tendsto_small_sets_iff Filter.tendsto_smallSets_iff

theorem eventually_smallSets {p : Set Î± â†’ Prop} :
    (âˆ€á¶  s in l.smallSets, p s) â†” âˆƒ s âˆˆ l, âˆ€ t, t âŠ† s â†’ p t :=
  eventually_lift'_iff monotone_powerset
#align filter.eventually_small_sets Filter.eventually_smallSets

theorem eventually_smallSets' {p : Set Î± â†’ Prop} (hp : âˆ€ â¦ƒs tâ¦„, s âŠ† t â†’ p t â†’ p s) :
    (âˆ€á¶  s in l.smallSets, p s) â†” âˆƒ s âˆˆ l, p s :=
  eventually_smallSets.trans <|
    exists_congr fun s => Iff.rfl.and âŸ¨fun H => H s Subset.rfl, fun hs _t ht => hp ht hsâŸ©
#align filter.eventually_small_sets' Filter.eventually_smallSets'

theorem frequently_smallSets {p : Set Î± â†’ Prop} :
    (âˆƒá¶  s in l.smallSets, p s) â†” âˆ€ t âˆˆ l, âˆƒ s, s âŠ† t âˆ§ p s :=
  l.hasBasis_smallSets.frequently_iff
#align filter.frequently_small_sets Filter.frequently_smallSets

theorem frequently_smallSets_mem (l : Filter Î±) : âˆƒá¶  s in l.smallSets, s âˆˆ l :=
  frequently_smallSets.2 fun t ht => âŸ¨t, Subset.rfl, htâŸ©
#align filter.frequently_small_sets_mem Filter.frequently_smallSets_mem

@[simp]
lemma tendsto_image_smallSets {f : Î± â†’ Î²} :
    Tendsto (f '' Â·) la.smallSets lb.smallSets â†” Tendsto f la lb := by
  rw [tendsto_smallSets_iff]
  refine forallâ‚‚_congr fun u hu â†¦ ?_
  rw [eventually_smallSets' fun s t hst ht â†¦ (image_subset _ hst).trans ht]
  simp only [image_subset_iff, exists_mem_subset_iff, mem_map]

alias âŸ¨_, Tendsto.image_smallSetsâŸ© := tendsto_image_smallSets

theorem HasAntitoneBasis.tendsto_smallSets {Î¹} [Preorder Î¹] {s : Î¹ â†’ Set Î±}
    (hl : l.HasAntitoneBasis s) : Tendsto s atTop l.smallSets :=
  tendsto_smallSets_iff.2 fun _t ht => hl.eventually_subset ht
#align filter.has_antitone_basis.tendsto_small_sets Filter.HasAntitoneBasis.tendsto_smallSets

@[mono]
theorem monotone_smallSets : Monotone (@smallSets Î±) :=
  monotone_lift' monotone_id monotone_const
#align filter.monotone_small_sets Filter.monotone_smallSets

@[simp]
theorem smallSets_bot : (âŠ¥ : Filter Î±).smallSets = pure âˆ… := by
  rw [smallSets, lift'_bot, powerset_empty, principal_singleton]
  exact monotone_powerset
#align filter.small_sets_bot Filter.smallSets_bot

@[simp]
theorem smallSets_top : (âŠ¤ : Filter Î±).smallSets = âŠ¤ := by
  rw [smallSets, lift'_top, powerset_univ, principal_univ]
#align filter.small_sets_top Filter.smallSets_top

@[simp]
theorem smallSets_principal (s : Set Î±) : (ğ“Ÿ s).smallSets = ğ“Ÿ (ğ’« s) :=
  lift'_principal monotone_powerset
#align filter.small_sets_principal Filter.smallSets_principal

theorem smallSets_comap_eq_comap_image (l : Filter Î²) (f : Î± â†’ Î²) :
    (comap f l).smallSets = comap (image f) l.smallSets := by
  refine (gc_map_comap _).u_comm_of_l_comm (gc_map_comap _) bind_smallSets_gc bind_smallSets_gc ?_
  simp [Function.comp, map_bind, bind_map]

theorem smallSets_comap (l : Filter Î²) (f : Î± â†’ Î²) :
    (comap f l).smallSets = l.lift' (powerset âˆ˜ preimage f) :=
  comap_lift'_eq2 monotone_powerset
#align filter.small_sets_comap Filter.smallSets_comap

theorem comap_smallSets (l : Filter Î²) (f : Î± â†’ Set Î²) :
    comap f l.smallSets = l.lift' (preimage f âˆ˜ powerset) :=
  comap_lift'_eq
#align filter.comap_small_sets Filter.comap_smallSets

theorem smallSets_iInf {f : Î¹ â†’ Filter Î±} : (iInf f).smallSets = â¨… i, (f i).smallSets :=
  lift'_iInf_of_map_univ (powerset_inter _ _) powerset_univ
#align filter.small_sets_infi Filter.smallSets_iInf

theorem smallSets_inf (lâ‚ lâ‚‚ : Filter Î±) : (lâ‚ âŠ“ lâ‚‚).smallSets = lâ‚.smallSets âŠ“ lâ‚‚.smallSets :=
  lift'_inf _ _ powerset_inter
#align filter.small_sets_inf Filter.smallSets_inf

instance smallSets_neBot (l : Filter Î±) : NeBot l.smallSets := by
  refine (lift'_neBot_iff ?_).2 fun _ _ => powerset_nonempty
  exact monotone_powerset
#align filter.small_sets_ne_bot Filter.smallSets_neBot

theorem Tendsto.smallSets_mono {s t : Î± â†’ Set Î²} (ht : Tendsto t la lb.smallSets)
    (hst : âˆ€á¶  x in la, s x âŠ† t x) : Tendsto s la lb.smallSets := by
  rw [tendsto_smallSets_iff] at ht âŠ¢
  exact fun u hu => (ht u hu).mp (hst.mono fun _ hst ht => hst.trans ht)
#align filter.tendsto.small_sets_mono Filter.Tendsto.smallSets_mono

/-- Generalized **squeeze theorem** (also known as **sandwich theorem**). If `s : Î± â†’ Set Î²` is a
family of sets that tends to `Filter.smallSets lb` along `la` and `f : Î± â†’ Î²` is a function such
that `f x âˆˆ s x` eventually along `la`, then `f` tends to `lb` along `la`.

If `s x` is the closed interval `[g x, h x]` for some functions `g`, `h` that tend to the same limit
`ğ“ y`, then we obtain the standard squeeze theorem, see
`tendsto_of_tendsto_of_tendsto_of_le_of_le'`. -/
theorem Tendsto.of_smallSets {s : Î± â†’ Set Î²} {f : Î± â†’ Î²} (hs : Tendsto s la lb.smallSets)
    (hf : âˆ€á¶  x in la, f x âˆˆ s x) : Tendsto f la lb := fun t ht =>
  hf.mp <| (tendsto_smallSets_iff.mp hs t ht).mono fun _ hâ‚ hâ‚‚ => hâ‚ hâ‚‚
#align filter.tendsto.of_small_sets Filter.Tendsto.of_smallSets

@[simp]
theorem eventually_smallSets_eventually {p : Î± â†’ Prop} :
    (âˆ€á¶  s in l.smallSets, âˆ€á¶  x in l', x âˆˆ s â†’ p x) â†” âˆ€á¶  x in l âŠ“ l', p x :=
  calc
    _ â†” âˆƒ s âˆˆ l, âˆ€á¶  x in l', x âˆˆ s â†’ p x :=
      eventually_smallSets' fun s t hst ht => ht.mono fun x hx hs => hx (hst hs)
    _ â†” âˆƒ s âˆˆ l, âˆƒ t âˆˆ l', âˆ€ x, x âˆˆ t â†’ x âˆˆ s â†’ p x := by simp only [eventually_iff_exists_mem]
    _ â†” âˆ€á¶  x in l âŠ“ l', p x := by simp only [eventually_inf, and_comm, mem_inter_iff, â† and_imp]
#align filter.eventually_small_sets_eventually Filter.eventually_smallSets_eventually

@[simp]
theorem eventually_smallSets_forall {p : Î± â†’ Prop} :
    (âˆ€á¶  s in l.smallSets, âˆ€ x âˆˆ s, p x) â†” âˆ€á¶  x in l, p x := by
  simpa only [inf_top_eq, eventually_top] using @eventually_smallSets_eventually Î± l âŠ¤ p
#align filter.eventually_small_sets_forall Filter.eventually_smallSets_forall

alias âŸ¨Eventually.of_smallSets, Eventually.smallSetsâŸ© := eventually_smallSets_forall
#align filter.eventually.of_small_sets Filter.Eventually.of_smallSets
#align filter.eventually.small_sets Filter.Eventually.smallSets

@[simp]
theorem eventually_smallSets_subset {s : Set Î±} : (âˆ€á¶  t in l.smallSets, t âŠ† s) â†” s âˆˆ l :=
  eventually_smallSets_forall
#align filter.eventually_small_sets_subset Filter.eventually_smallSets_subset

end Filter
