/-
Copyright (c) 2022 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Floris van Doorn, Yury Kudryashov
-/
import Mathlib.Order.Filter.Lift
import Mathlib.Order.Filter.AtTopBot.Basic

/-!
# The filter of small sets

This file defines the filter of small sets w.r.t. a filter `f`, which is the largest filter
containing all powersets of members of `f`.

`g` converges to `f.smallSets` if for all `s âˆˆ f`, eventually we have `g x âŠ† s`.

An example usage is that if `f : Î¹ â†’ E â†’ â„` is a family of nonnegative functions with integral 1,
then saying that `fun i â†¦ support (f i)` tendsto `(ğ“ 0).smallSets` is a way of saying that
`f` tends to the Dirac delta distribution.
-/


open Filter

open Set

variable {Î± Î² : Type*} {Î¹ : Sort*}

namespace Filter

variable {l l' la : Filter Î±} {lb : Filter Î²}

/-- The filter `l.smallSets` is the largest filter containing all powersets of members of `l`. -/
def smallSets (l : Filter Î±) : Filter (Set Î±) :=
  l.lift' powerset

theorem smallSets_eq_generate {f : Filter Î±} : f.smallSets = generate (powerset '' f.sets) := by
  simp_rw [generate_eq_biInf, smallSets, iInf_image, Filter.lift', Filter.lift, Function.comp_apply,
    Filter.mem_sets]

-- TODO: get more properties from the adjunction?
-- TODO: is there a general way to get a lower adjoint for the lift of an upper adjoint?
theorem bind_smallSets_gc :
    GaloisConnection (fun L : Filter (Set Î±) â†¦ L.bind principal) smallSets := by
  intro L l
  simp_rw [smallSets_eq_generate, le_generate_iff, image_subset_iff]
  rfl

protected theorem HasBasis.smallSets {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±} (h : HasBasis l p s) :
    HasBasis l.smallSets p fun i => ğ’« s i :=
  h.lift' monotone_powerset

theorem hasBasis_smallSets (l : Filter Î±) :
    HasBasis l.smallSets (fun t : Set Î± => t âˆˆ l) powerset :=
  l.basis_sets.smallSets

theorem Eventually.exists_mem_basis_of_smallSets {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±} {P : Set Î± â†’ Prop}
    (hâ‚ : âˆ€á¶  t in l.smallSets, P t) (hâ‚‚ : HasBasis l p s) : âˆƒ i, p i âˆ§ P (s i) :=
  (hâ‚‚.smallSets.eventually_iff.mp hâ‚).imp fun _i âŸ¨hpi, hiâŸ© â†¦ âŸ¨hpi, hi Subset.rflâŸ©

theorem Frequently.smallSets_of_forall_mem_basis {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±} {P : Set Î± â†’ Prop}
    (hâ‚ : âˆ€ i, p i â†’ P (s i)) (hâ‚‚ : HasBasis l p s) : âˆƒá¶  t in l.smallSets, P t :=
  hâ‚‚.smallSets.frequently_iff.mpr fun _ hi => âŸ¨_, Subset.rfl, hâ‚ _ hiâŸ©

theorem Eventually.exists_mem_of_smallSets {p : Set Î± â†’ Prop}
    (h : âˆ€á¶  t in l.smallSets, p t) : âˆƒ s âˆˆ l, p s :=
  h.exists_mem_basis_of_smallSets l.basis_sets

/-! No `Frequently.smallSets_of_forall_mem (h : âˆ€ s âˆˆ l, p s) : âˆƒá¶  t in l.smallSets, p t` as
`Filter.frequently_smallSets_mem : âˆƒá¶  t in l.smallSets, t âˆˆ l` is preferred. -/

/-- `g` converges to `f.smallSets` if for all `s âˆˆ f`, eventually we have `g x âŠ† s`. -/
theorem tendsto_smallSets_iff {f : Î± â†’ Set Î²} :
    Tendsto f la lb.smallSets â†” âˆ€ t âˆˆ lb, âˆ€á¶  x in la, f x âŠ† t :=
  (hasBasis_smallSets lb).tendsto_right_iff

theorem eventually_smallSets {p : Set Î± â†’ Prop} :
    (âˆ€á¶  s in l.smallSets, p s) â†” âˆƒ s âˆˆ l, âˆ€ t, t âŠ† s â†’ p t :=
  eventually_lift'_iff monotone_powerset

theorem eventually_smallSets' {p : Set Î± â†’ Prop} (hp : âˆ€ â¦ƒs tâ¦„, s âŠ† t â†’ p t â†’ p s) :
    (âˆ€á¶  s in l.smallSets, p s) â†” âˆƒ s âˆˆ l, p s :=
  eventually_smallSets.trans <|
    exists_congr fun s => Iff.rfl.and âŸ¨fun H => H s Subset.rfl, fun hs _t ht => hp ht hsâŸ©

theorem frequently_smallSets {p : Set Î± â†’ Prop} :
    (âˆƒá¶  s in l.smallSets, p s) â†” âˆ€ t âˆˆ l, âˆƒ s, s âŠ† t âˆ§ p s :=
  l.hasBasis_smallSets.frequently_iff

theorem frequently_smallSets_mem (l : Filter Î±) : âˆƒá¶  s in l.smallSets, s âˆˆ l :=
  frequently_smallSets.2 fun t ht => âŸ¨t, Subset.rfl, htâŸ©

@[simp]
lemma tendsto_image_smallSets {f : Î± â†’ Î²} :
    Tendsto (f '' Â·) la.smallSets lb.smallSets â†” Tendsto f la lb := by
  rw [tendsto_smallSets_iff]
  refine forallâ‚‚_congr fun u hu â†¦ ?_
  rw [eventually_smallSets' fun s t hst ht â†¦ (image_subset _ hst).trans ht]
  simp only [image_subset_iff, exists_mem_subset_iff, mem_map]

alias âŸ¨_, Tendsto.image_smallSetsâŸ© := tendsto_image_smallSets

theorem HasAntitoneBasis.tendsto_smallSets {Î¹} [Preorder Î¹] {s : Î¹ â†’ Set Î±}
    (hl : l.HasAntitoneBasis s) : Tendsto s atTop l.smallSets :=
  tendsto_smallSets_iff.2 fun _t ht => hl.eventually_subset ht

@[mono]
theorem monotone_smallSets : Monotone (@smallSets Î±) :=
  monotone_lift' monotone_id monotone_const

@[simp]
theorem smallSets_bot : (âŠ¥ : Filter Î±).smallSets = pure âˆ… := by
  rw [smallSets, lift'_bot, powerset_empty, principal_singleton]
  exact monotone_powerset

@[simp]
theorem smallSets_top : (âŠ¤ : Filter Î±).smallSets = âŠ¤ := by
  rw [smallSets, lift'_top, powerset_univ, principal_univ]

@[simp]
theorem smallSets_principal (s : Set Î±) : (ğ“Ÿ s).smallSets = ğ“Ÿ (ğ’« s) :=
  lift'_principal monotone_powerset

theorem smallSets_comap_eq_comap_image (l : Filter Î²) (f : Î± â†’ Î²) :
    (comap f l).smallSets = comap (image f) l.smallSets := by
  refine (gc_map_comap _).u_comm_of_l_comm (gc_map_comap _) bind_smallSets_gc bind_smallSets_gc ?_
  simp [Function.comp_def, map_bind, bind_map]

theorem smallSets_comap (l : Filter Î²) (f : Î± â†’ Î²) :
    (comap f l).smallSets = l.lift' (powerset âˆ˜ preimage f) :=
  comap_lift'_eq2 monotone_powerset

theorem comap_smallSets (l : Filter Î²) (f : Î± â†’ Set Î²) :
    comap f l.smallSets = l.lift' (preimage f âˆ˜ powerset) :=
  comap_lift'_eq

theorem smallSets_iInf {f : Î¹ â†’ Filter Î±} : (iInf f).smallSets = â¨… i, (f i).smallSets :=
  lift'_iInf_of_map_univ (powerset_inter _ _) powerset_univ

theorem smallSets_inf (lâ‚ lâ‚‚ : Filter Î±) : (lâ‚ âŠ“ lâ‚‚).smallSets = lâ‚.smallSets âŠ“ lâ‚‚.smallSets :=
  lift'_inf _ _ powerset_inter

instance smallSets_neBot (l : Filter Î±) : NeBot l.smallSets := by
  refine (lift'_neBot_iff ?_).2 fun _ _ => powerset_nonempty
  exact monotone_powerset

theorem Tendsto.smallSets_mono {s t : Î± â†’ Set Î²} (ht : Tendsto t la lb.smallSets)
    (hst : âˆ€á¶  x in la, s x âŠ† t x) : Tendsto s la lb.smallSets := by
  rw [tendsto_smallSets_iff] at ht âŠ¢
  exact fun u hu => (ht u hu).mp (hst.mono fun _ hst ht => hst.trans ht)

/-- Generalized **squeeze theorem** (also known as **sandwich theorem**). If `s : Î± â†’ Set Î²` is a
family of sets that tends to `Filter.smallSets lb` along `la` and `f : Î± â†’ Î²` is a function such
that `f x âˆˆ s x` eventually along `la`, then `f` tends to `lb` along `la`.

If `s x` is the closed interval `[g x, h x]` for some functions `g`, `h` that tend to the same limit
`ğ“ y`, then we obtain the standard squeeze theorem, see
`tendsto_of_tendsto_of_tendsto_of_le_of_le'`. -/
theorem Tendsto.of_smallSets {s : Î± â†’ Set Î²} {f : Î± â†’ Î²} (hs : Tendsto s la lb.smallSets)
    (hf : âˆ€á¶  x in la, f x âˆˆ s x) : Tendsto f la lb := fun t ht =>
  hf.mp <| (tendsto_smallSets_iff.mp hs t ht).mono fun _ hâ‚ hâ‚‚ => hâ‚ hâ‚‚

@[simp]
theorem eventually_smallSets_eventually {p : Î± â†’ Prop} :
    (âˆ€á¶  s in l.smallSets, âˆ€á¶  x in l', x âˆˆ s â†’ p x) â†” âˆ€á¶  x in l âŠ“ l', p x :=
  calc
    _ â†” âˆƒ s âˆˆ l, âˆ€á¶  x in l', x âˆˆ s â†’ p x :=
      eventually_smallSets' fun _ _ hst ht => ht.mono fun _ hx hs => hx (hst hs)
    _ â†” âˆƒ s âˆˆ l, âˆƒ t âˆˆ l', âˆ€ x, x âˆˆ t â†’ x âˆˆ s â†’ p x := by simp only [eventually_iff_exists_mem]
    _ â†” âˆ€á¶  x in l âŠ“ l', p x := by simp only [eventually_inf, and_comm, mem_inter_iff, â† and_imp]

@[simp]
theorem eventually_smallSets_forall {p : Î± â†’ Prop} :
    (âˆ€á¶  s in l.smallSets, âˆ€ x âˆˆ s, p x) â†” âˆ€á¶  x in l, p x := by
  simpa only [inf_top_eq, eventually_top] using @eventually_smallSets_eventually Î± l âŠ¤ p

alias âŸ¨Eventually.of_smallSets, Eventually.smallSetsâŸ© := eventually_smallSets_forall

@[simp]
theorem eventually_smallSets_subset {s : Set Î±} : (âˆ€á¶  t in l.smallSets, t âŠ† s) â†” s âˆˆ l :=
  eventually_smallSets_forall

end Filter
