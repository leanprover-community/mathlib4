/-
Copyright (c) 2020 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov
-/
import Mathlib.Order.Filter.AtTopBot

#align_import order.filter.indicator_function from "leanprover-community/mathlib"@"8631e2d5ea77f6c13054d9151d82b83069680cb1"

/-!
# Indicator function and filters

Properties of additive and multiplicative indicator functions involving `=·∂†` and `‚â§·∂†`.

## Tags
indicator, characteristic, filter
-/

variable {Œ± Œ≤ M E : Type*}

open Set Filter

section One

variable [One M] {s t : Set Œ±} {f g : Œ± ‚Üí M} {a : Œ±} {l : Filter Œ±}

@[to_additive]
theorem mulIndicator_eventuallyEq (hf : f =·∂†[l ‚äì ùìü s] g) (hs : s =·∂†[l] t) :
    mulIndicator s f =·∂†[l] mulIndicator t g :=
  (eventually_inf_principal.1 hf).mp <| hs.mem_iff.mono fun x hst hfg =>
    by_cases
      (fun hxs : x ‚àà s => by simp only [*, hst.1 hxs, mulIndicator_of_mem])
      (fun hxs => by simp only [mulIndicator_of_not_mem, hxs, mt hst.2 hxs, not_false_eq_true])
#align indicator_eventually_eq indicator_eventuallyEq

end One

section Monoid

variable [Monoid M] {s t : Set Œ±} {f g : Œ± ‚Üí M} {a : Œ±} {l : Filter Œ±}

@[to_additive]
theorem mulIndicator_union_eventuallyEq (h : ‚àÄ·∂† a in l, a ‚àâ s ‚à© t) :
    mulIndicator (s ‚à™ t) f =·∂†[l] mulIndicator s f * mulIndicator t f :=
  h.mono fun _a ha => mulIndicator_union_of_not_mem_inter ha _
#align indicator_union_eventually_eq indicator_union_eventuallyEq

end Monoid

section Order

variable [One Œ≤] [Preorder Œ≤] {s t : Set Œ±} {f g : Œ± ‚Üí Œ≤} {a : Œ±} {l : Filter Œ±}

@[to_additive]
theorem mulIndicator_eventuallyLE_mulIndicator (h : f ‚â§·∂†[l ‚äì ùìü s] g) :
    mulIndicator s f ‚â§·∂†[l] mulIndicator s g :=
  (eventually_inf_principal.1 h).mono fun _ => mulIndicator_rel_mulIndicator le_rfl
#align indicator_eventually_le_indicator indicator_eventuallyLE_indicator

end Order

@[to_additive]
theorem Monotone.mulIndicator_eventuallyEq_iUnion {Œπ} [Preorder Œπ] [One Œ≤] (s : Œπ ‚Üí Set Œ±)
    (hs : Monotone s) (f : Œ± ‚Üí Œ≤) (a : Œ±) :
    (fun i => mulIndicator (s i) f a) =·∂†[atTop] fun _ ‚Ü¶ mulIndicator (‚ãÉ i, s i) f a := by
  classical exact hs.piecewise_eventually_eq_iUnion f 1 a

@[to_additive]
theorem Monotone.tendsto_mulIndicator {Œπ} [Preorder Œπ] [One Œ≤] (s : Œπ ‚Üí Set Œ±) (hs : Monotone s)
    (f : Œ± ‚Üí Œ≤) (a : Œ±) :
    Tendsto (fun i => mulIndicator (s i) f a) atTop (pure <| mulIndicator (‚ãÉ i, s i) f a) :=
  tendsto_pure.2 <| hs.mulIndicator_eventuallyEq_iUnion s f a
#align monotone.tendsto_indicator Monotone.tendsto_indicator

@[to_additive]
theorem Antitone.mulIndicator_eventuallyEq_iInter {Œπ} [Preorder Œπ] [One Œ≤] (s : Œπ ‚Üí Set Œ±)
    (hs : Antitone s) (f : Œ± ‚Üí Œ≤) (a : Œ±) :
    (fun i => mulIndicator (s i) f a) =·∂†[atTop] fun _ ‚Ü¶ mulIndicator (‚ãÇ i, s i) f a := by
  classical exact hs.piecewise_eventually_eq_iInter f 1 a

@[to_additive]
theorem Antitone.tendsto_mulIndicator {Œπ} [Preorder Œπ] [One Œ≤] (s : Œπ ‚Üí Set Œ±) (hs : Antitone s)
    (f : Œ± ‚Üí Œ≤) (a : Œ±) :
    Tendsto (fun i => mulIndicator (s i) f a) atTop (pure <| mulIndicator (‚ãÇ i, s i) f a) :=
  tendsto_pure.2 <| hs.mulIndicator_eventuallyEq_iInter s f a
#align antitone.tendsto_indicator Antitone.tendsto_indicator

@[to_additive]
theorem mulIndicator_biUnion_finset_eventuallyEq {Œπ} [One Œ≤] (s : Œπ ‚Üí Set Œ±) (f : Œ± ‚Üí Œ≤) (a : Œ±) :
    (fun n : Finset Œπ => mulIndicator (‚ãÉ i ‚àà n, s i) f a) =·∂†[atTop]
      fun _ ‚Ü¶ mulIndicator (iUnion s) f a := by
  rw [iUnion_eq_iUnion_finset s]
  apply Monotone.mulIndicator_eventuallyEq_iUnion
  exact fun _ _ ‚Ü¶ biUnion_subset_biUnion_left

@[to_additive]
theorem tendsto_mulIndicator_biUnion_finset {Œπ} [One Œ≤] (s : Œπ ‚Üí Set Œ±) (f : Œ± ‚Üí Œ≤) (a : Œ±) :
    Tendsto (fun n : Finset Œπ => mulIndicator (‚ãÉ i ‚àà n, s i) f a) atTop
      (pure <| mulIndicator (iUnion s) f a) :=
  tendsto_pure.2 <| mulIndicator_biUnion_finset_eventuallyEq s f a
#align tendsto_indicator_bUnion_finset tendsto_indicator_biUnion_finset

@[to_additive]
protected theorem Filter.EventuallyEq.mulSupport [One Œ≤] {f g : Œ± ‚Üí Œ≤} {l : Filter Œ±}
    (h : f =·∂†[l] g) :
    Function.mulSupport f =·∂†[l] Function.mulSupport g :=
  h.preimage ({1}·∂ú : Set Œ≤)
#align filter.eventually_eq.support Filter.EventuallyEq.support

@[to_additive]
protected theorem Filter.EventuallyEq.mulIndicator [One Œ≤] {l : Filter Œ±} {f g : Œ± ‚Üí Œ≤} {s : Set Œ±}
    (hfg : f =·∂†[l] g) : s.mulIndicator f =·∂†[l] s.mulIndicator g :=
  mulIndicator_eventuallyEq (hfg.filter_mono inf_le_left) EventuallyEq.rfl
#align filter.eventually_eq.indicator Filter.EventuallyEq.indicator

@[to_additive]
theorem Filter.EventuallyEq.mulIndicator_one [One Œ≤] {l : Filter Œ±} {f : Œ± ‚Üí Œ≤} {s : Set Œ±}
    (hf : f =·∂†[l] 1) : s.mulIndicator f =·∂†[l] 1 :=
  hf.mulIndicator.trans <| by rw [mulIndicator_one']
#align filter.eventually_eq.indicator_zero Filter.EventuallyEq.indicator_zero

@[to_additive]
theorem Filter.EventuallyEq.of_mulIndicator [One Œ≤] {l : Filter Œ±} {f : Œ± ‚Üí Œ≤}
    (hf : ‚àÄ·∂† x in l, f x ‚â† 1) {s t : Set Œ±} (h : s.mulIndicator f =·∂†[l] t.mulIndicator f) :
    s =·∂†[l] t := by
  have : ‚àÄ {s : Set Œ±}, Function.mulSupport (s.mulIndicator f) =·∂†[l] s := fun {s} ‚Ü¶ by
    rw [mulSupport_mulIndicator]
    exact (hf.mono fun x hx ‚Ü¶ and_iff_left hx).set_eq
  exact this.symm.trans <| h.mulSupport.trans this

@[to_additive]
theorem Filter.EventuallyEq.of_mulIndicator_const [One Œ≤] {l : Filter Œ±} {c : Œ≤} (hc : c ‚â† 1)
    {s t : Set Œ±} (h : s.mulIndicator (fun _ ‚Ü¶ c) =·∂†[l] t.mulIndicator fun _ ‚Ü¶ c) : s =·∂†[l] t :=
  .of_mulIndicator (eventually_of_forall fun _ ‚Ü¶ hc) h
