/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Jeremy Avigad
-/
import Mathlib.Data.Set.Insert
import Mathlib.Order.SetNotation
import Mathlib.Order.BooleanAlgebra.Set
import Mathlib.Order.Bounds.Defs

/-!
# Definitions about filters

A *filter* on a type `Î±` is a collection of sets of `Î±` which contains the whole `Î±`,
is upwards-closed, and is stable under intersection. Filters are mostly used to
abstract two related kinds of ideas:
* *limits*, including finite or infinite limits of sequences, finite or infinite limits of functions
  at a point or at infinity, etc...
* *things happening eventually*, including things happening for large enough `n : â„•`, or near enough
  a point `x`, or for close enough pairs of points, or things happening almost everywhere in the
  sense of measure theory. Dually, filters can also express the idea of *things happening often*:
  for arbitrarily large `n`, or at a point in any neighborhood of given a point etc...

## Main definitions

* `Filter` : filters on a set;
* `Filter.principal`, `ğ“Ÿ s` : filter of all sets containing a given set;
* `Filter.map`, `Filter.comap` : operations on filters;
* `Filter.Tendsto` : limit with respect to filters;
* `Filter.Eventually` : `f.Eventually p` means `{x | p x} âˆˆ f`;
* `Filter.Frequently` : `f.Frequently p` means `{x | Â¬p x} âˆ‰ f`;
* `filter_upwards [hâ‚, ..., hâ‚™]` :
  a tactic that takes a list of proofs `háµ¢ : sáµ¢ âˆˆ f`,
  and replaces a goal `s âˆˆ f` with `âˆ€ x, x âˆˆ sâ‚ â†’ ... â†’ x âˆˆ sâ‚™ â†’ x âˆˆ s`;
* `Filter.NeBot f` : a utility class stating that `f` is a non-trivial filter.
* `Filter.IsBounded r f`: the filter `f` is eventually bounded w.r.t. the relation `r`,
  i.e. eventually, it is bounded by some uniform bound.
  `r` will be usually instantiated with `(Â· â‰¤ Â·)` or `(Â· â‰¥ Â·)`.
* `Filter.IsCobounded r f` states that the filter `f` does not tend to infinity w.r.t. `r`.
  This is also called frequently bounded. Will be usually instantiated with `(Â· â‰¤ Â·)` or `(Â· â‰¥ Â·)`.

## Notations

* `âˆ€á¶  x in f, p x` : `f.Eventually p`;
* `âˆƒá¶  x in f, p x` : `f.Frequently p`;
* `f =á¶ [l] g` : `âˆ€á¶  x in l, f x = g x`;
* `f â‰¤á¶ [l] g` : `âˆ€á¶  x in l, f x â‰¤ g x`;
* `ğ“Ÿ s` : `Filter.Principal s`, localized in `Filter`.

## Implementation Notes

Important note: Bourbaki requires that a filter on `X` cannot contain all sets of `X`,
which we do *not* require.
This gives `Filter X` better formal properties,
in particular a bottom element `âŠ¥` for its lattice structure,
at the cost of including the assumption `[NeBot f]` in a number of lemmas and definitions.

## References

*  [N. Bourbaki, *General Topology*][bourbaki1966]
-/

assert_not_exists RelIso

open Set

/-- A filter `F` on a type `Î±` is a collection of sets of `Î±` which contains the whole `Î±`,
is upwards-closed, and is stable under intersection. We do not forbid this collection to be
all sets of `Î±`. -/
structure Filter (Î± : Type*) where
  /-- The set of sets that belong to the filter. -/
  sets : Set (Set Î±)
  /-- The set `Set.univ` belongs to any filter. -/
  univ_sets : Set.univ âˆˆ sets
  /-- If a set belongs to a filter, then its superset belongs to the filter as well. -/
  sets_of_superset {x y} : x âˆˆ sets â†’ x âŠ† y â†’ y âˆˆ sets
  /-- If two sets belong to a filter, then their intersection belongs to the filter as well. -/
  inter_sets {x y} : x âˆˆ sets â†’ y âˆˆ sets â†’ x âˆ© y âˆˆ sets

namespace Filter

variable {Î± Î² : Type*} {f g : Filter Î±} {s t : Set Î±}

theorem filter_eq : âˆ€ {f g : Filter Î±}, f.sets = g.sets â†’ f = g
  | âŸ¨_, _, _, _âŸ©, âŸ¨_, _, _, _âŸ©, rfl => rfl

/-- If `F` is a filter on `Î±`, and `U` a subset of `Î±` then we can write `U âˆˆ F` as on paper. -/
instance instMembership : Membership (Set Î±) (Filter Î±) := âŸ¨fun F U => U âˆˆ F.setsâŸ©

@[ext]
protected theorem ext (h : âˆ€ s, s âˆˆ f â†” s âˆˆ g) : f = g := filter_eq <| Set.ext h

@[simp]
protected theorem mem_mk {t : Set (Set Î±)} {hâ‚ hâ‚‚ hâ‚ƒ} : s âˆˆ mk t hâ‚ hâ‚‚ hâ‚ƒ â†” s âˆˆ t :=
  Iff.rfl

@[simp]
protected theorem mem_sets : s âˆˆ f.sets â†” s âˆˆ f :=
  Iff.rfl

@[simp]
theorem univ_mem : univ âˆˆ f :=
  f.univ_sets

theorem mem_of_superset {x y : Set Î±} (hx : x âˆˆ f) (hxy : x âŠ† y) : y âˆˆ f :=
  f.sets_of_superset hx hxy

theorem univ_mem' (h : âˆ€ a, a âˆˆ s) : s âˆˆ f :=
  mem_of_superset univ_mem fun x _ => h x

theorem inter_mem (hs : s âˆˆ f) (ht : t âˆˆ f) : s âˆ© t âˆˆ f :=
  f.inter_sets hs ht

theorem mp_mem (hs : s âˆˆ f) (h : { x | x âˆˆ s â†’ x âˆˆ t } âˆˆ f) : t âˆˆ f :=
  mem_of_superset (inter_mem hs h) fun _ âŸ¨hâ‚, hâ‚‚âŸ© => hâ‚‚ hâ‚

/-- Override `sets` field of a filter to provide better definitional equality. -/
protected def copy (f : Filter Î±) (S : Set (Set Î±)) (hmem : âˆ€ s, s âˆˆ S â†” s âˆˆ f) : Filter Î± where
  sets := S
  univ_sets := (hmem _).2 univ_mem
  sets_of_superset h hsub := (hmem _).2 <| mem_of_superset ((hmem _).1 h) hsub
  inter_sets hâ‚ hâ‚‚ := (hmem _).2 <| inter_mem ((hmem _).1 hâ‚) ((hmem _).1 hâ‚‚)

@[simp] theorem mem_copy {S hmem} : s âˆˆ f.copy S hmem â†” s âˆˆ S := Iff.rfl

/-- Construct a filter from a property that is stable under finite unions.
A set `s` belongs to `Filter.comk p _ _ _` iff its complement satisfies the predicate `p`.
This constructor is useful to define filters like `Filter.cofinite`. -/
def comk (p : Set Î± â†’ Prop) (he : p âˆ…) (hmono : âˆ€ t, p t â†’ âˆ€ s âŠ† t, p s)
    (hunion : âˆ€ s, p s â†’ âˆ€ t, p t â†’ p (s âˆª t)) : Filter Î± where
  sets := {t | p tá¶œ}
  univ_sets := by simpa
  sets_of_superset := fun htâ‚ ht => hmono _ htâ‚ _ (compl_subset_compl.2 ht)
  inter_sets := fun htâ‚ htâ‚‚ => by simp [compl_inter, hunion _ htâ‚ _ htâ‚‚]

@[simp]
lemma mem_comk {p : Set Î± â†’ Prop} {he hmono hunion s} :
    s âˆˆ comk p he hmono hunion â†” p sá¶œ :=
  .rfl

/-- The principal filter of `s` is the collection of all supersets of `s`. -/
def principal (s : Set Î±) : Filter Î± where
  sets := { t | s âŠ† t }
  univ_sets := subset_univ s
  sets_of_superset hx := Subset.trans hx
  inter_sets := subset_inter

@[inherit_doc]
scoped notation "ğ“Ÿ" => Filter.principal

@[simp] theorem mem_principal : s âˆˆ ğ“Ÿ t â†” t âŠ† s := Iff.rfl

/-- `pure x` is the set of sets that contain `x`. It is equal to `ğ“Ÿ {x}` but
with this definition we have `s âˆˆ pure a` defeq `a âˆˆ s`. -/
instance : Pure Filter where
  pure x := .copy (ğ“Ÿ {x}) {s | x âˆˆ s} fun _ â†¦ by simp

@[simp]
theorem mem_pure {a : Î±} {s : Set Î±} : s âˆˆ (pure a : Filter Î±) â†” a âˆˆ s :=
  Iff.rfl

/-- The *kernel* of a filter is the intersection of all its sets. -/
def ker (f : Filter Î±) : Set Î± := â‹‚â‚€ f.sets

/-- The join of a filter of filters is defined by the relation `s âˆˆ join f â†” {t | s âˆˆ t} âˆˆ f`. -/
def join (f : Filter (Filter Î±)) : Filter Î± where
  sets := { s | { t : Filter Î± | s âˆˆ t } âˆˆ f }
  univ_sets := by simp only [mem_setOf_eq, univ_mem, setOf_true]
  sets_of_superset hx xy := mem_of_superset hx fun f h => mem_of_superset h xy
  inter_sets hx hy := mem_of_superset (inter_mem hx hy) fun f âŸ¨hâ‚, hâ‚‚âŸ© => inter_mem hâ‚ hâ‚‚

@[simp]
theorem mem_join {s : Set Î±} {f : Filter (Filter Î±)} : s âˆˆ join f â†” { t | s âˆˆ t } âˆˆ f :=
  Iff.rfl

instance : PartialOrder (Filter Î±) where
  le f g := âˆ€ â¦ƒU : Set Î±â¦„, U âˆˆ g â†’ U âˆˆ f
  le_antisymm a b hâ‚ hâ‚‚ := filter_eq <| Subset.antisymm hâ‚‚ hâ‚
  le_refl a := Subset.rfl
  le_trans a b c hâ‚ hâ‚‚ := Subset.trans hâ‚‚ hâ‚

theorem le_def : f â‰¤ g â†” âˆ€ x âˆˆ g, x âˆˆ f :=
  Iff.rfl

instance instSupSet : SupSet (Filter Î±) where
  sSup S := join (ğ“Ÿ S)

@[simp] theorem mem_sSup {S : Set (Filter Î±)} : s âˆˆ sSup S â†” âˆ€ f âˆˆ S, s âˆˆ f := .rfl

/-- Infimum of a set of filters.
This definition is marked as irreducible
so that Lean doesn't try to unfold it when unifying expressions. -/
@[irreducible]
protected def sInf (s : Set (Filter Î±)) : Filter Î± := sSup (lowerBounds s)

instance instInfSet : InfSet (Filter Î±) where
  sInf := Filter.sInf

protected theorem sSup_lowerBounds (s : Set (Filter Î±)) : sSup (lowerBounds s) = sInf s := by
  simp [sInf, Filter.sInf]

instance : Top (Filter Î±) where
  top := .copy (sSup (Set.range pure)) {s | âˆ€ x, x âˆˆ s} <| by simp

theorem mem_top_iff_forall {s : Set Î±} : s âˆˆ (âŠ¤ : Filter Î±) â†” âˆ€ x, x âˆˆ s :=
  Iff.rfl

@[simp]
theorem mem_top {s : Set Î±} : s âˆˆ (âŠ¤ : Filter Î±) â†” s = univ := by
  rw [mem_top_iff_forall, eq_univ_iff_forall]

instance : Bot (Filter Î±) where
  bot := .copy (sSup âˆ…) univ <| by simp

@[simp]
theorem mem_bot {s : Set Î±} : s âˆˆ (âŠ¥ : Filter Î±) :=
  trivial

/-- The infimum of filters is the filter generated by intersections
  of elements of the two filters. -/
instance instInf : Min (Filter Î±) :=
  âŸ¨fun f g : Filter Î± =>
    { sets := { s | âˆƒ a âˆˆ f, âˆƒ b âˆˆ g, s = a âˆ© b }
      univ_sets := âŸ¨_, univ_mem, _, univ_mem, by simpâŸ©
      sets_of_superset := by
        rintro x y âŸ¨a, ha, b, hb, rflâŸ© xy
        refine âŸ¨a âˆª y, mem_of_superset ha subset_union_left, b âˆª y,
          mem_of_superset hb subset_union_left, ?_âŸ©
        rw [â† inter_union_distrib_right, union_eq_self_of_subset_left xy]
      inter_sets := by
        rintro x y âŸ¨a, ha, b, hb, rflâŸ© âŸ¨c, hc, d, hd, rflâŸ©
        refine âŸ¨a âˆ© c, inter_mem ha hc, b âˆ© d, inter_mem hb hd, ?_âŸ©
        ac_rfl }âŸ©

/-- The supremum of two filters is the filter that contains sets that belong to both filters. -/
instance instSup : Max (Filter Î±) where
  max f g := .copy (sSup {f, g}) {s | s âˆˆ f âˆ§ s âˆˆ g} <| by simp

/-- A filter is `NeBot` if it is not equal to `âŠ¥`, or equivalently the empty set does not belong to
the filter. Bourbaki include this assumption in the definition of a filter but we prefer to have a
`CompleteLattice` structure on `Filter _`, so we use a typeclass argument in lemmas instead. -/
class NeBot (f : Filter Î±) : Prop where
  /-- The filter is nontrivial: `f â‰  âŠ¥` or equivalently, `âˆ… âˆ‰ f`. -/
  ne' : f â‰  âŠ¥

theorem neBot_iff {f : Filter Î±} : NeBot f â†” f â‰  âŠ¥ :=
  âŸ¨fun h => h.1, fun h => âŸ¨hâŸ©âŸ©

/-- `f.Eventually p` or `âˆ€á¶  x in f, p x` mean that `{x | p x} âˆˆ f`. E.g., `âˆ€á¶  x in atTop, p x`
means that `p` holds true for sufficiently large `x`. -/
protected def Eventually (p : Î± â†’ Prop) (f : Filter Î±) : Prop :=
  { x | p x } âˆˆ f

@[inherit_doc Filter.Eventually]
notation3 "âˆ€á¶  "(...)" in "f", "r:(scoped p => Filter.Eventually p f) => r

/-- `f.Frequently p` or `âˆƒá¶  x in f, p x` mean that `{x | Â¬p x} âˆ‰ f`. E.g., `âˆƒá¶  x in atTop, p x`
means that there exist arbitrarily large `x` for which `p` holds true. -/
protected def Frequently (p : Î± â†’ Prop) (f : Filter Î±) : Prop :=
  Â¬âˆ€á¶  x in f, Â¬p x

@[inherit_doc Filter.Frequently]
notation3 "âˆƒá¶  "(...)" in "f", "r:(scoped p => Filter.Frequently p f) => r

/-- Two functions `f` and `g` are *eventually equal* along a filter `l` if the set of `x` such that
`f x = g x` belongs to `l`. -/
def EventuallyEq (l : Filter Î±) (f g : Î± â†’ Î²) : Prop :=
  âˆ€á¶  x in l, f x = g x

@[inherit_doc]
notation:50 f " =á¶ [" l:50 "] " g:50 => EventuallyEq l f g

/-- A function `f` is eventually less than or equal to a function `g` at a filter `l`. -/
def EventuallyLE [LE Î²] (l : Filter Î±) (f g : Î± â†’ Î²) : Prop :=
  âˆ€á¶  x in l, f x â‰¤ g x

@[inherit_doc]
notation:50 f " â‰¤á¶ [" l:50 "] " g:50 => EventuallyLE l f g

/-- The forward map of a filter -/
def map (m : Î± â†’ Î²) (f : Filter Î±) : Filter Î² where
  sets := preimage m â»Â¹' f.sets
  univ_sets := univ_mem
  sets_of_superset hs st := mem_of_superset hs fun _x hx â†¦ st hx
  inter_sets hs ht := inter_mem hs ht

/-- `Filter.Tendsto` is the generic "limit of a function" predicate.
  `Tendsto f lâ‚ lâ‚‚` asserts that for every `lâ‚‚` neighborhood `a`,
  the `f`-preimage of `a` is an `lâ‚` neighborhood. -/
def Tendsto (f : Î± â†’ Î²) (lâ‚ : Filter Î±) (lâ‚‚ : Filter Î²) :=
  lâ‚.map f â‰¤ lâ‚‚

/-- The inverse map of a filter. A set `s` belongs to `Filter.comap m f` if either of the following
equivalent conditions hold.

1. There exists a set `t âˆˆ f` such that `m â»Â¹' t âŠ† s`. This is used as a definition.
2. The set `kernImage m s = {y | âˆ€ x, m x = y â†’ x âˆˆ s}` belongs to `f`, see `Filter.mem_comap'`.
3. The set `(m '' sá¶œ)á¶œ` belongs to `f`, see `Filter.mem_comap_iff_compl` and
`Filter.compl_mem_comap`. -/
def comap (m : Î± â†’ Î²) (f : Filter Î²) : Filter Î± where
  sets := { s | âˆƒ t âˆˆ f, m â»Â¹' t âŠ† s }
  univ_sets := âŸ¨univ, univ_mem, subset_univ _âŸ©
  sets_of_superset := fun âŸ¨a', ha', ma'aâŸ© ab => âŸ¨a', ha', ma'a.trans abâŸ©
  inter_sets := fun âŸ¨a', haâ‚, haâ‚‚âŸ© âŸ¨b', hbâ‚, hbâ‚‚âŸ© =>
    âŸ¨a' âˆ© b', inter_mem haâ‚ hbâ‚, inter_subset_inter haâ‚‚ hbâ‚‚âŸ©

/-- Coproduct of filters. -/
protected def coprod (f : Filter Î±) (g : Filter Î²) : Filter (Î± Ã— Î²) :=
  f.comap Prod.fst âŠ” g.comap Prod.snd

/-- Product of filters. This is the filter generated by Cartesian products
of elements of the component filters. -/
instance instSProd : SProd (Filter Î±) (Filter Î²) (Filter (Î± Ã— Î²)) where
  sprod f g := f.comap Prod.fst âŠ“ g.comap Prod.snd

theorem prod_eq_inf (f : Filter Î±) (g : Filter Î²) : f Ã—Ë¢ g = f.comap Prod.fst âŠ“ g.comap Prod.snd :=
  rfl

/-- The product of an indexed family of filters. -/
def pi {Î¹ : Type*} {Î± : Î¹ â†’ Type*} (f : âˆ€ i, Filter (Î± i)) : Filter (âˆ€ i, Î± i) :=
  â¨… i, comap (Function.eval i) (f i)

/-- The monadic bind operation on filter is defined the usual way in terms of `map` and `join`.

Unfortunately, this `bind` does not result in the expected applicative. See `Filter.seq` for the
applicative instance. -/
def bind (f : Filter Î±) (m : Î± â†’ Filter Î²) : Filter Î² :=
  join (map m f)

/-- The applicative sequentiation operation. This is not induced by the bind operation. -/
def seq (f : Filter (Î± â†’ Î²)) (g : Filter Î±) : Filter Î² where
  sets := { s | âˆƒ u âˆˆ f, âˆƒ t âˆˆ g, âˆ€ m âˆˆ u, âˆ€ x âˆˆ t, (m : Î± â†’ Î²) x âˆˆ s }
  univ_sets := âŸ¨univ, univ_mem, univ, univ_mem, fun _ _ _ _ => trivialâŸ©
  sets_of_superset := fun âŸ¨tâ‚€, tâ‚, hâ‚€, hâ‚, hâŸ© hst =>
    âŸ¨tâ‚€, tâ‚, hâ‚€, hâ‚, fun _ hx _ hy => hst <| h _ hx _ hyâŸ©
  inter_sets := fun âŸ¨tâ‚€, htâ‚€, tâ‚, htâ‚, htâŸ© âŸ¨uâ‚€, huâ‚€, uâ‚, huâ‚, huâŸ© =>
    âŸ¨tâ‚€ âˆ© uâ‚€, inter_mem htâ‚€ huâ‚€, tâ‚ âˆ© uâ‚, inter_mem htâ‚ huâ‚, fun _ âŸ¨hxâ‚€, hxâ‚âŸ© _ âŸ¨hyâ‚€, hyâ‚âŸ© =>
      âŸ¨ht _ hxâ‚€ _ hyâ‚€, hu _ hxâ‚ _ hyâ‚âŸ©âŸ©

/-- This filter is characterized by `Filter.eventually_curry_iff`:
`(âˆ€á¶  (x : Î± Ã— Î²) in f.curry g, p x) â†” âˆ€á¶  (x : Î±) in f, âˆ€á¶  (y : Î²) in g, p (x, y)`. Useful
in adding quantifiers to the middle of `Tendsto`s. See
`hasFDerivAt_of_tendstoUniformlyOnFilter`. -/
def curry (f : Filter Î±) (g : Filter Î²) : Filter (Î± Ã— Î²) :=
  bind f fun a â†¦ map (a, Â·) g

instance : Bind Filter :=
  âŸ¨@Filter.bindâŸ©

instance : Functor Filter where map := @Filter.map

/-- A variant on `bind` using a function `g` taking a set instead of a member of `Î±`.
This is essentially a push-forward along a function mapping each set to a filter. -/
protected def lift (f : Filter Î±) (g : Set Î± â†’ Filter Î²) :=
  â¨… s âˆˆ f, g s

/-- Specialize `lift` to functions `Set Î± â†’ Set Î²`. This can be viewed as a generalization of `map`.
This is essentially a push-forward along a function mapping each set to a set. -/
protected def lift' (f : Filter Î±) (h : Set Î± â†’ Set Î²) :=
  f.lift (ğ“Ÿ âˆ˜ h)

/-- `f.IsBounded r`: the filter `f` is eventually bounded w.r.t. the relation `r`,
i.e. eventually, it is bounded by some uniform bound.
`r` will be usually instantiated with `(Â· â‰¤ Â·)` or `(Â· â‰¥ Â·)`. -/
def IsBounded (r : Î± â†’ Î± â†’ Prop) (f : Filter Î±) :=
  âˆƒ b, âˆ€á¶  x in f, r x b

/-- `f.IsBoundedUnder (â‰º) u`: the image of the filter `f` under `u` is eventually bounded w.r.t.
the relation `â‰º`, i.e. eventually, it is bounded by some uniform bound. -/
def IsBoundedUnder (r : Î± â†’ Î± â†’ Prop) (f : Filter Î²) (u : Î² â†’ Î±) :=
  (map u f).IsBounded r

/-- `IsCobounded (â‰º) f` states that the filter `f` does not tend to infinity w.r.t. `â‰º`. This is
also called frequently bounded. Will be usually instantiated with `â‰¤` or `â‰¥`.

There is a subtlety in this definition: we want `f.IsCobounded` to hold for any `f` in the case of
complete lattices. This will be relevant to deduce theorems on complete lattices from their
versions on conditionally complete lattices with additional assumptions. We have to be careful in
the edge case of the trivial filter containing the empty set: the other natural definition
  `Â¬ âˆ€ a, âˆ€á¶  n in f, a â‰¤ n`
would not work as well in this case.
-/
def IsCobounded (r : Î± â†’ Î± â†’ Prop) (f : Filter Î±) :=
  âˆƒ b, âˆ€ a, (âˆ€á¶  x in f, r x a) â†’ r b a

/-- `IsCoboundedUnder (â‰º) f u` states that the image of the filter `f` under the map `u` does not
tend to infinity w.r.t. `â‰º`. This is also called frequently bounded. Will be usually instantiated
with `â‰¤` or `â‰¥`. -/
def IsCoboundedUnder (r : Î± â†’ Î± â†’ Prop) (f : Filter Î²) (u : Î² â†’ Î±) :=
  (map u f).IsCobounded r

end Filter

namespace Mathlib.Tactic

open Lean Meta Elab Tactic

/--
`filter_upwards [hâ‚, â‹¯, hâ‚™]` replaces a goal of the form `s âˆˆ f` and terms
`hâ‚ : tâ‚ âˆˆ f, â‹¯, hâ‚™ : tâ‚™ âˆˆ f` with `âˆ€ x, x âˆˆ tâ‚ â†’ â‹¯ â†’ x âˆˆ tâ‚™ â†’ x âˆˆ s`.
The list is an optional parameter, `[]` being its default value.

`filter_upwards [hâ‚, â‹¯, hâ‚™] with aâ‚ aâ‚‚ â‹¯ aâ‚–` is a short form for
`{ filter_upwards [hâ‚, â‹¯, hâ‚™], intro aâ‚ aâ‚‚ â‹¯ aâ‚– }`.

`filter_upwards [hâ‚, â‹¯, hâ‚™] using e` is a short form for
`{ filter_upwards [h1, â‹¯, hn], exact e }`.

Combining both shortcuts is done by writing `filter_upwards [hâ‚, â‹¯, hâ‚™] with aâ‚ aâ‚‚ â‹¯ aâ‚– using e`.
Note that in this case, the `aáµ¢` terms can be used in `e`.
-/
syntax (name := filterUpwards) "filter_upwards" (" [" term,* "]")?
  (" with" (ppSpace colGt term:max)*)? (" using " term)? : tactic

elab_rules : tactic
| `(tactic| filter_upwards $[[$[$args],*]]? $[with $wth*]? $[using $usingArg]?) => do
  focus do
    let config : ApplyConfig := {newGoals := ApplyNewGoals.nonDependentOnly}
    for e in args.getD #[] |>.reverse do
      let goal â† getMainGoal
      replaceMainGoal <| â† goal.withContext <| runTermElab do
        let m â† mkFreshExprMVar none
        let lem â† Term.elabTermEnsuringType
          (â† ``(Filter.mp_mem $e $(â† Term.exprToSyntax m))) (â† goal.getType)
        goal.assign lem
        return [m.mvarId!]
    liftMetaTactic fun goal => do
      goal.apply (â† mkConstWithFreshMVarLevels ``Filter.univ_mem') config
    evalTactic <|â† `(tactic| dsimp -zeta only [Set.mem_setOf_eq])
    if let some l := wth then
      evalTactic <|â† `(tactic| intro $[$l]*)
    if let some e := usingArg then
      evalTactic <|â† `(tactic| exact $e)

end Mathlib.Tactic
