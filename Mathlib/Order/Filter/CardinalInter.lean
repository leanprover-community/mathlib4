/-
Copyright (c) 2024 Josha Dekker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Josha Dekker
-/
import Mathlib.Order.Filter.Tendsto
import Mathlib.Order.Filter.Finite
import Mathlib.Order.Filter.CountableInter
import Mathlib.SetTheory.Cardinal.Regular
import Mathlib.Tactic.Linarith

/-!
# Filters with a cardinal intersection property

In this file we define `CardinalInterFilter l c` to be the class of filters with the following
property: for any collection of sets `s ‚àà l` with cardinality strictly less than `c`,
their intersection belongs to `l` as well.

# Main results
* `Filter.cardinalInterFilter_aleph0` establishes that every filter `l` is a
    `CardinalInterFilter l ‚Ñµ‚ÇÄ`
* `CardinalInterFilter.toCountableInterFilter` establishes that every `CardinalInterFilter l c` with
    `c > ‚Ñµ‚ÇÄ` is a `CountableInterFilter`.
* `CountableInterFilter.toCardinalInterFilter` establishes that every `CountableInterFilter l` is a
    `CardinalInterFilter l ‚Ñµ‚ÇÅ`.
* `CardinalInterFilter.of_CardinalInterFilter_of_lt` establishes that we have
  `CardinalInterFilter l c` ‚Üí `CardinalInterFilter l a` for all `a < c`.

## Tags
filter, cardinal
-/


open Set Filter Cardinal

universe u
variable {Œπ : Type u} {Œ± Œ≤ : Type u} {c : Cardinal.{u}}

/-- A filter `l` has the cardinal `c` intersection property if for any collection
of less than `c` sets `s ‚àà l`, their intersection belongs to `l` as well. -/
class CardinalInterFilter (l : Filter Œ±) (c : Cardinal.{u}) : Prop where
  /-- For a collection of sets `s ‚àà l` with cardinality below c,
  their intersection belongs to `l` as well. -/
  cardinal_sInter_mem : ‚àÄ S : Set (Set Œ±), (#S < c) ‚Üí (‚àÄ s ‚àà S, s ‚àà l) ‚Üí ‚ãÇ‚ÇÄ S ‚àà l

variable {l : Filter Œ±}

theorem cardinal_sInter_mem {S : Set (Set Œ±)} [CardinalInterFilter l c] (hSc : #S < c) :
    ‚ãÇ‚ÇÄ S ‚àà l ‚Üî ‚àÄ s ‚àà S, s ‚àà l := ‚ü®fun hS _s hs => mem_of_superset hS (sInter_subset_of_mem hs),
  CardinalInterFilter.cardinal_sInter_mem _ hSc‚ü©

/-- Every filter is a CardinalInterFilter with c = ‚Ñµ‚ÇÄ -/
theorem _root_.Filter.cardinalInterFilter_aleph0 (l : Filter Œ±) : CardinalInterFilter l ‚Ñµ‚ÇÄ where
  cardinal_sInter_mem := by
    simp_all only [aleph_zero, lt_aleph0_iff_subtype_finite, setOf_mem_eq, sInter_mem,
      implies_true, forall_const]

/-- Every CardinalInterFilter with c > ‚Ñµ‚ÇÄ is a CountableInterFilter -/
theorem CardinalInterFilter.toCountableInterFilter (l : Filter Œ±) [CardinalInterFilter l c]
    (hc : ‚Ñµ‚ÇÄ < c) : CountableInterFilter l where
  countable_sInter_mem S hS a :=
    CardinalInterFilter.cardinal_sInter_mem S (lt_of_le_of_lt (Set.Countable.le_aleph0 hS) hc) a

/-- Every CountableInterFilter is a CardinalInterFilter with c = ‚Ñµ‚ÇÅ -/
instance CountableInterFilter.toCardinalInterFilter (l : Filter Œ±) [CountableInterFilter l] :
    CardinalInterFilter l ‚Ñµ‚ÇÅ where
  cardinal_sInter_mem S hS a :=
    CountableInterFilter.countable_sInter_mem S ((countable_iff_lt_aleph_one S).mpr hS) a

theorem cardinalInterFilter_aleph_one_iff :
    CardinalInterFilter l ‚Ñµ‚ÇÅ ‚Üî CountableInterFilter l :=
  ‚ü®fun _ ‚Ü¶ ‚ü®fun S h a ‚Ü¶
    CardinalInterFilter.cardinal_sInter_mem S ((countable_iff_lt_aleph_one S).1 h) a‚ü©,
   fun _ ‚Ü¶ CountableInterFilter.toCardinalInterFilter l‚ü©

/-- Every CardinalInterFilter for some c also is a CardinalInterFilter for some a ‚â§ c -/
theorem CardinalInterFilter.of_cardinalInterFilter_of_le (l : Filter Œ±) [CardinalInterFilter l c]
    {a : Cardinal.{u}} (hac : a ‚â§ c) :
    CardinalInterFilter l a where
  cardinal_sInter_mem S hS a :=
    CardinalInterFilter.cardinal_sInter_mem S (lt_of_lt_of_le hS hac) a

theorem CardinalInterFilter.of_cardinalInterFilter_of_lt (l : Filter Œ±) [CardinalInterFilter l c]
    {a : Cardinal.{u}} (hac : a < c) : CardinalInterFilter l a :=
  CardinalInterFilter.of_cardinalInterFilter_of_le l (hac.le)

namespace Filter

variable [CardinalInterFilter l c]

theorem cardinal_iInter_mem {s : Œπ ‚Üí Set Œ±} (hic : #Œπ < c) :
    (‚ãÇ i, s i) ‚àà l ‚Üî ‚àÄ i, s i ‚àà l := by
  rw [‚Üê sInter_range _]
  apply (cardinal_sInter_mem (lt_of_le_of_lt Cardinal.mk_range_le hic)).trans
  exact forall_mem_range

theorem cardinal_bInter_mem {S : Set Œπ} (hS : #S < c)
    {s : ‚àÄ i ‚àà S, Set Œ±} :
    (‚ãÇ i, ‚ãÇ hi : i ‚àà S, s i ‚Äπ_‚Ä∫) ‚àà l ‚Üî ‚àÄ i, ‚àÄ hi : i ‚àà S, s i ‚Äπ_‚Ä∫ ‚àà l := by
  rw [biInter_eq_iInter]
  exact (cardinal_iInter_mem hS).trans Subtype.forall

theorem eventually_cardinal_forall {p : Œ± ‚Üí Œπ ‚Üí Prop} (hic : #Œπ < c) :
    (‚àÄ·∂† x in l, ‚àÄ i, p x i) ‚Üî ‚àÄ i, ‚àÄ·∂† x in l, p x i := by
  simp only [Filter.Eventually, setOf_forall]
  exact cardinal_iInter_mem hic

theorem eventually_cardinal_ball {S : Set Œπ} (hS : #S < c)
    {p : Œ± ‚Üí ‚àÄ i ‚àà S, Prop} :
    (‚àÄ·∂† x in l, ‚àÄ i hi, p x i hi) ‚Üî ‚àÄ i hi, ‚àÄ·∂† x in l, p x i hi := by
  simp only [Filter.Eventually, setOf_forall]
  exact cardinal_bInter_mem hS

theorem EventuallyLE.cardinal_iUnion {s t : Œπ ‚Üí Set Œ±} (hic : #Œπ < c)
    (h : ‚àÄ i, s i ‚â§·∂†[l] t i) : ‚ãÉ i, s i ‚â§·∂†[l] ‚ãÉ i, t i :=
  ((eventually_cardinal_forall hic).2 h).mono fun _ hst hs => mem_iUnion.2 <|
    (mem_iUnion.1 hs).imp hst

theorem EventuallyEq.cardinal_iUnion {s t : Œπ ‚Üí Set Œ±} (hic : #Œπ < c)
    (h : ‚àÄ i, s i =·∂†[l] t i) : ‚ãÉ i, s i =·∂†[l] ‚ãÉ i, t i :=
  (EventuallyLE.cardinal_iUnion hic fun i => (h i).le).antisymm
    (EventuallyLE.cardinal_iUnion hic fun i => (h i).symm.le)

theorem EventuallyLE.cardinal_bUnion {S : Set Œπ} (hS : #S < c)
    {s t : ‚àÄ i ‚àà S, Set Œ±} (h : ‚àÄ i hi, s i hi ‚â§·∂†[l] t i hi) :
    ‚ãÉ i ‚àà S, s i ‚Äπ_‚Ä∫ ‚â§·∂†[l] ‚ãÉ i ‚àà S, t i ‚Äπ_‚Ä∫ := by
  simp only [biUnion_eq_iUnion]
  exact EventuallyLE.cardinal_iUnion hS fun i => h i i.2

theorem EventuallyEq.cardinal_bUnion {S : Set Œπ} (hS : #S < c)
    {s t : ‚àÄ i ‚àà S, Set Œ±} (h : ‚àÄ i hi, s i hi =·∂†[l] t i hi) :
    ‚ãÉ i ‚àà S, s i ‚Äπ_‚Ä∫ =·∂†[l] ‚ãÉ i ‚àà S, t i ‚Äπ_‚Ä∫ :=
  (EventuallyLE.cardinal_bUnion hS fun i hi => (h i hi).le).antisymm
    (EventuallyLE.cardinal_bUnion hS fun i hi => (h i hi).symm.le)

theorem EventuallyLE.cardinal_iInter {s t : Œπ ‚Üí Set Œ±} (hic : #Œπ < c)
    (h : ‚àÄ i, s i ‚â§·∂†[l] t i) : ‚ãÇ i, s i ‚â§·∂†[l] ‚ãÇ i, t i :=
  ((eventually_cardinal_forall hic).2 h).mono fun _ hst hs =>
    mem_iInter.2 fun i => hst _ (mem_iInter.1 hs i)

theorem EventuallyEq.cardinal_iInter {s t : Œπ ‚Üí Set Œ±} (hic : #Œπ < c)
    (h : ‚àÄ i, s i =·∂†[l] t i) : ‚ãÇ i, s i =·∂†[l] ‚ãÇ i, t i :=
  (EventuallyLE.cardinal_iInter hic fun i => (h i).le).antisymm
    (EventuallyLE.cardinal_iInter hic fun i => (h i).symm.le)

theorem EventuallyLE.cardinal_bInter {S : Set Œπ} (hS : #S < c)
    {s t : ‚àÄ i ‚àà S, Set Œ±} (h : ‚àÄ i hi, s i hi ‚â§·∂†[l] t i hi) :
    ‚ãÇ i ‚àà S, s i ‚Äπ_‚Ä∫ ‚â§·∂†[l] ‚ãÇ i ‚àà S, t i ‚Äπ_‚Ä∫ := by
  simp only [biInter_eq_iInter]
  exact EventuallyLE.cardinal_iInter hS fun i => h i i.2

theorem EventuallyEq.cardinal_bInter {S : Set Œπ} (hS : #S < c)
    {s t : ‚àÄ i ‚àà S, Set Œ±} (h : ‚àÄ i hi, s i hi =·∂†[l] t i hi) :
    ‚ãÇ i ‚àà S, s i ‚Äπ_‚Ä∫ =·∂†[l] ‚ãÇ i ‚àà S, t i ‚Äπ_‚Ä∫ :=
  (EventuallyLE.cardinal_bInter hS fun i hi => (h i hi).le).antisymm
    (EventuallyLE.cardinal_bInter hS fun i hi => (h i hi).symm.le)

/-- Construct a filter with cardinal `c` intersection property. This constructor deduces
`Filter.univ_sets` and `Filter.inter_sets` from the cardinal `c` intersection property. -/
def ofCardinalInter (l : Set (Set Œ±)) (hc : 2 < c)
    (hl : ‚àÄ S : Set (Set Œ±), (#S < c) ‚Üí S ‚äÜ l ‚Üí ‚ãÇ‚ÇÄ S ‚àà l)
    (h_mono : ‚àÄ s t, s ‚àà l ‚Üí s ‚äÜ t ‚Üí t ‚àà l) : Filter Œ± where
  sets := l
  univ_sets :=
    sInter_empty ‚ñ∏ hl ‚àÖ (mk_eq_zero (‚àÖ : Set (Set Œ±)) ‚ñ∏ lt_trans zero_lt_two hc) (empty_subset _)
  sets_of_superset := h_mono _ _
  inter_sets {s t} hs ht := sInter_pair s t ‚ñ∏ by
    apply hl _ (?_) (insert_subset_iff.2 ‚ü®hs, singleton_subset_iff.2 ht‚ü©)
    have : #({s, t} : Set (Set Œ±)) ‚â§ 2 := by
      calc
      _ ‚â§ #({t} : Set (Set Œ±)) + 1 := Cardinal.mk_insert_le
      _ = 2 := by norm_num
    exact lt_of_le_of_lt this hc

instance cardinalInter_ofCardinalInter (l : Set (Set Œ±)) (hc : 2 < c)
    (hl : ‚àÄ S : Set (Set Œ±), (#S < c) ‚Üí S ‚äÜ l ‚Üí ‚ãÇ‚ÇÄ S ‚àà l)
    (h_mono : ‚àÄ s t, s ‚àà l ‚Üí s ‚äÜ t ‚Üí t ‚àà l) :
    CardinalInterFilter (Filter.ofCardinalInter l hc hl h_mono) c :=
  ‚ü®hl‚ü©

@[simp]
theorem mem_ofCardinalInter {l : Set (Set Œ±)} (hc : 2 < c)
    (hl : ‚àÄ S : Set (Set Œ±), (#S < c) ‚Üí S ‚äÜ l ‚Üí ‚ãÇ‚ÇÄ S ‚àà l) (h_mono : ‚àÄ s t, s ‚àà l ‚Üí s ‚äÜ t ‚Üí t ‚àà l)
    {s : Set Œ±} : s ‚àà Filter.ofCardinalInter l hc hl h_mono ‚Üî s ‚àà l :=
  Iff.rfl

/-- Construct a filter with cardinal `c` intersection property.
Similarly to `Filter.comk`, a set belongs to this filter if its complement satisfies the property.
Similarly to `Filter.ofCardinalInter`,
this constructor deduces some properties from the cardinal `c` intersection property
which becomes the cardinal `c` union property because we take complements of all sets. -/
def ofCardinalUnion (l : Set (Set Œ±)) (hc : 2 < c)
    (hUnion : ‚àÄ S : Set (Set Œ±), (#S < c) ‚Üí (‚àÄ s ‚àà S, s ‚àà l) ‚Üí ‚ãÉ‚ÇÄ S ‚àà l)
    (hmono : ‚àÄ t ‚àà l, ‚àÄ s ‚äÜ t, s ‚àà l) : Filter Œ± := by
  refine .ofCardinalInter {s | s·∂ú ‚àà l} hc (fun S hSc hSp ‚Ü¶ ?_) fun s t ht hsub ‚Ü¶ ?_
  ¬∑ rw [mem_setOf_eq, compl_sInter]
    apply hUnion (compl '' S) (lt_of_le_of_lt mk_image_le hSc)
    intro s hs
    rw [mem_image] at hs
    rcases hs with ‚ü®t, ht, rfl‚ü©
    apply hSp ht
  ¬∑ rw [mem_setOf_eq]
    rw [‚Üê compl_subset_compl] at hsub
    exact hmono s·∂ú ht t·∂ú hsub

instance cardinalInter_ofCardinalUnion (l : Set (Set Œ±)) (hc : 2 < c) (h‚ÇÅ h‚ÇÇ) :
    CardinalInterFilter (Filter.ofCardinalUnion l hc h‚ÇÅ h‚ÇÇ) c :=
  cardinalInter_ofCardinalInter ..

@[simp]
theorem mem_ofCardinalUnion {l : Set (Set Œ±)} (hc : 2 < c) {hunion hmono s} :
    s ‚àà ofCardinalUnion l hc hunion hmono ‚Üî l s·∂ú :=
  Iff.rfl

instance cardinalInterFilter_principal (s : Set Œ±) : CardinalInterFilter (ùìü s) c :=
  ‚ü®fun _ _ hS => subset_sInter hS‚ü©

instance cardinalInterFilter_bot : CardinalInterFilter (‚ä• : Filter Œ±) c := by
  rw [‚Üê principal_empty]
  apply cardinalInterFilter_principal

instance cardinalInterFilter_top : CardinalInterFilter (‚ä§ : Filter Œ±) c := by
  rw [‚Üê principal_univ]
  apply cardinalInterFilter_principal

instance (l : Filter Œ≤) [CardinalInterFilter l c] (f : Œ± ‚Üí Œ≤) :
    CardinalInterFilter (comap f l) c := by
  refine ‚ü®fun S hSc hS => ?_‚ü©
  choose! t htl ht using hS
  refine ‚ü®_, (cardinal_bInter_mem hSc).2 htl, ?_‚ü©
  simpa [preimage_iInter] using iInter‚ÇÇ_mono ht

instance (l : Filter Œ±) [CardinalInterFilter l c] (f : Œ± ‚Üí Œ≤) :
    CardinalInterFilter (map f l) c := by
  refine ‚ü®fun S hSc hS => ?_‚ü©
  simp only [mem_map, sInter_eq_biInter, preimage_iInter‚ÇÇ] at hS ‚ä¢
  exact (cardinal_bInter_mem hSc).2 hS

/-- Infimum of two `CardinalInterFilter`s is a `CardinalInterFilter`. This is useful, e.g.,
to automatically get an instance for `residual Œ± ‚äì ùìü s`. -/
instance cardinalInterFilter_inf_eq (l‚ÇÅ l‚ÇÇ : Filter Œ±) [CardinalInterFilter l‚ÇÅ c]
    [CardinalInterFilter l‚ÇÇ c] : CardinalInterFilter (l‚ÇÅ ‚äì l‚ÇÇ) c := by
  refine ‚ü®fun S hSc hS => ?_‚ü©
  choose s hs t ht hst using hS
  replace hs : (‚ãÇ i ‚àà S, s i ‚Äπ_‚Ä∫) ‚àà l‚ÇÅ := (cardinal_bInter_mem hSc).2 hs
  replace ht : (‚ãÇ i ‚àà S, t i ‚Äπ_‚Ä∫) ‚àà l‚ÇÇ := (cardinal_bInter_mem hSc).2 ht
  refine mem_of_superset (inter_mem_inf hs ht) (subset_sInter fun i hi => ?_)
  rw [hst i hi]
  apply inter_subset_inter <;> exact iInter_subset_of_subset i (iInter_subset _ _)

instance cardinalInterFilter_inf (l‚ÇÅ l‚ÇÇ : Filter Œ±) {c‚ÇÅ c‚ÇÇ : Cardinal.{u}}
    [CardinalInterFilter l‚ÇÅ c‚ÇÅ] [CardinalInterFilter l‚ÇÇ c‚ÇÇ] : CardinalInterFilter (l‚ÇÅ ‚äì l‚ÇÇ)
    (c‚ÇÅ ‚äì c‚ÇÇ) := by
  have : CardinalInterFilter l‚ÇÅ (c‚ÇÅ ‚äì c‚ÇÇ) :=
    CardinalInterFilter.of_cardinalInterFilter_of_le l‚ÇÅ inf_le_left
  have : CardinalInterFilter l‚ÇÇ (c‚ÇÅ ‚äì c‚ÇÇ) :=
    CardinalInterFilter.of_cardinalInterFilter_of_le l‚ÇÇ inf_le_right
  exact cardinalInterFilter_inf_eq _ _

/-- Supremum of two `CardinalInterFilter`s is a `CardinalInterFilter`. -/
instance cardinalInterFilter_sup_eq (l‚ÇÅ l‚ÇÇ : Filter Œ±) [CardinalInterFilter l‚ÇÅ c]
    [CardinalInterFilter l‚ÇÇ c] : CardinalInterFilter (l‚ÇÅ ‚äî l‚ÇÇ) c := by
  refine ‚ü®fun S hSc hS => ‚ü®?_, ?_‚ü©‚ü© <;> refine (cardinal_sInter_mem hSc).2 fun s hs => ?_
  exacts [(hS s hs).1, (hS s hs).2]

instance cardinalInterFilter_sup (l‚ÇÅ l‚ÇÇ : Filter Œ±) {c‚ÇÅ c‚ÇÇ : Cardinal.{u}}
    [CardinalInterFilter l‚ÇÅ c‚ÇÅ] [CardinalInterFilter l‚ÇÇ c‚ÇÇ] :
    CardinalInterFilter (l‚ÇÅ ‚äî l‚ÇÇ) (c‚ÇÅ ‚äì c‚ÇÇ) := by
  have : CardinalInterFilter l‚ÇÅ (c‚ÇÅ ‚äì c‚ÇÇ) :=
    CardinalInterFilter.of_cardinalInterFilter_of_le l‚ÇÅ inf_le_left
  have : CardinalInterFilter l‚ÇÇ (c‚ÇÅ ‚äì c‚ÇÇ) :=
    CardinalInterFilter.of_cardinalInterFilter_of_le l‚ÇÇ inf_le_right
  exact cardinalInterFilter_sup_eq _ _

variable (g : Set (Set Œ±))

/-- `Filter.CardinalGenerateSets c g` is the (sets of the)
greatest `cardinalInterFilter c` containing `g`. -/
inductive CardinalGenerateSets : Set Œ± ‚Üí Prop
  | basic {s : Set Œ±} : s ‚àà g ‚Üí CardinalGenerateSets s
  | univ : CardinalGenerateSets univ
  | superset {s t : Set Œ±} : CardinalGenerateSets s ‚Üí s ‚äÜ t ‚Üí CardinalGenerateSets t
  | sInter {S : Set (Set Œ±)} :
    (#S < c) ‚Üí (‚àÄ s ‚àà S, CardinalGenerateSets s) ‚Üí CardinalGenerateSets (‚ãÇ‚ÇÄ S)

/-- `Filter.cardinalGenerate c g` is the greatest `cardinalInterFilter c` containing `g`. -/
def cardinalGenerate (hc : 2 < c) : Filter Œ± :=
  ofCardinalInter (CardinalGenerateSets g) hc (fun _ => CardinalGenerateSets.sInter) fun _ _ =>
    CardinalGenerateSets.superset

lemma cardinalInter_ofCardinalGenerate (hc : 2 < c) :
    CardinalInterFilter (cardinalGenerate g hc) c := by
  delta cardinalGenerate
  apply cardinalInter_ofCardinalInter _ _ _

variable {g}

/-- A set is in the `cardinalInterFilter` generated by `g` if and only if
it contains an intersection of `c` elements of `g`. -/
theorem mem_cardinaleGenerate_iff {s : Set Œ±} {hreg : c.IsRegular} :
    s ‚àà cardinalGenerate g (IsRegular.nat_lt hreg 2) ‚Üî
    ‚àÉ S : Set (Set Œ±), S ‚äÜ g ‚àß (#S < c) ‚àß ‚ãÇ‚ÇÄ S ‚äÜ s := by
  constructor <;> intro h
  ¬∑ induction h with
    | @basic s hs =>
      refine ‚ü®{s}, singleton_subset_iff.mpr hs, ?_‚ü©
      simpa [subset_refl] using IsRegular.nat_lt hreg 1
    | univ =>
      exact ‚ü®‚àÖ, ‚ü®empty_subset g, mk_eq_zero (‚àÖ : Set <| Set Œ±) ‚ñ∏ IsRegular.nat_lt hreg 0, by simp‚ü©‚ü©
    | superset _ _ ih => exact Exists.imp (by tauto) ih
    | @sInter S Sct _ ih =>
      choose T Tg Tct hT using ih
      refine ‚ü®‚ãÉ (s) (H : s ‚àà S), T s H, by simpa,
        (Cardinal.card_biUnion_lt_iff_forall_of_isRegular hreg Sct).2 Tct, ?_‚ü©
      apply subset_sInter
      apply fun s H => subset_trans (sInter_subset_sInter (subset_iUnion‚ÇÇ s H)) (hT s H)
  rcases h with ‚ü®S, Sg, Sct, hS‚ü©
  have : CardinalInterFilter (cardinalGenerate g (IsRegular.nat_lt hreg 2)) c :=
    cardinalInter_ofCardinalGenerate _ _
  exact mem_of_superset ((cardinal_sInter_mem Sct).mpr
    (fun s H => CardinalGenerateSets.basic (Sg H))) hS

theorem le_cardinalGenerate_iff_of_cardinalInterFilter {f : Filter Œ±} [CardinalInterFilter f c]
    (hc : 2 < c) : f ‚â§ cardinalGenerate g hc ‚Üî g ‚äÜ f.sets := by
  constructor <;> intro h
  ¬∑ exact subset_trans (fun s => CardinalGenerateSets.basic) h
  intro s hs
  induction hs with
  | basic hs => exact h hs
  | univ => exact univ_mem
  | superset _ st ih => exact mem_of_superset ih st
  | sInter Sct _ ih => exact (cardinal_sInter_mem Sct).mpr ih

/-- `cardinalGenerate g hc` is the greatest `cardinalInterFilter c` containing `g`. -/
theorem cardinalGenerate_isGreatest (hc : 2 < c) :
    IsGreatest { f : Filter Œ± | CardinalInterFilter f c ‚àß g ‚äÜ f.sets } (cardinalGenerate g hc) := by
  refine ‚ü®‚ü®cardinalInter_ofCardinalGenerate _ _, fun s => CardinalGenerateSets.basic‚ü©, ?_‚ü©
  rintro f ‚ü®fct, hf‚ü©
  rwa [le_cardinalGenerate_iff_of_cardinalInterFilter]

end Filter
