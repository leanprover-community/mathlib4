/-
Copyright (c) 2024 Josha Dekker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Josha Dekker
-/
import Mathlib.Order.Filter.Basic
import Mathlib.Order.Filter.CountableInter
import Mathlib.SetTheory.Cardinal.Ordinal
import Mathlib.SetTheory.Cardinal.Cofinality

/-!
# Filters with a cardinal intersection property

In this file we define `CardinalInterFilter l c` to be the class of filters with the following
property: for any collection of sets `s âˆˆ l` with cardinality strictly less than `c`,
their intersection belongs to `l` as well.

# Main results
* `Filter.cardinalInterFilter_aleph0` establishes that every filter `l` is a
    `CardinalInterFilter l aleph0`
* `CardinalInterFilter.toCountableInterFilter` establishes that every `CardinalInterFilter l c` with
    `c > aleph0` is a `CountableInterFilter`.
* `CountableInterFilter.toCardinalInterFilter` establishes that every `CountableInterFilter l` is a
    `CardinalInterFilter l aleph1`.
* `CardinalInterFilter.of_CardinalInterFilter_of_lt` establishes that we have
  `CardinalInterFilter l c` â†’ `CardinalInterFilter l a` for all `a < c`.

## Tags
filter, cardinal
-/


open Set Filter Cardinal

universe u
variable {Î¹ : Type u} {Î± Î² : Type u} {c : Cardinal.{u}}

/-- A filter `l` has the cardinal `c` intersection property if for any collection
of less than `c` sets `s âˆˆ l`, their intersection belongs to `l` as well. -/
class CardinalInterFilter (l : Filter Î±) (c : Cardinal.{u}) : Prop where
  /-- For a collection of sets `s âˆˆ l` with cardinality below c,
  their intersection belongs to `l` as well. -/
  cardinal_sInter_mem : âˆ€ S : Set (Set Î±), (#S < c) â†’ (âˆ€ s âˆˆ S, s âˆˆ l) â†’ â‹‚â‚€ S âˆˆ l

variable {l : Filter Î±}

theorem cardinal_sInter_mem {S : Set (Set Î±)} [CardinalInterFilter l c] (hSc : #S < c) :
    â‹‚â‚€ S âˆˆ l â†” âˆ€ s âˆˆ S, s âˆˆ l := âŸ¨fun hS _s hs => mem_of_superset hS (sInter_subset_of_mem hs),
  CardinalInterFilter.cardinal_sInter_mem _ hScâŸ©

/-- Every filter is a CardinalInterFilter with c = aleph0 -/
theorem _root_.Filter.cardinalInterFilter_aleph0 (l : Filter Î±) : CardinalInterFilter l aleph0 where
  cardinal_sInter_mem := by
    simp_all only [aleph_zero, lt_aleph0_iff_subtype_finite, setOf_mem_eq, sInter_mem,
      implies_true, forall_const]

/-- Every CardinalInterFilter with c > aleph0 is a CountableInterFilter -/
theorem CardinalInterFilter.toCountableInterFilter (l : Filter Î±) [CardinalInterFilter l c]
    (hc : aleph0 < c) : CountableInterFilter l where
  countable_sInter_mem := by
    intro S hS
    exact fun a â†¦ CardinalInterFilter.cardinal_sInter_mem S
      (lt_of_le_of_lt (Set.Countable.le_aleph0 hS) hc) a

/-- Every CountableInterFilter is a CardinalInterFilter with c = aleph 1-/
instance CountableInterFilter.toCardinalInterFilter (l : Filter Î±) [CountableInterFilter l] :
    CardinalInterFilter l (aleph 1) where
  cardinal_sInter_mem := fun S hS a â†¦ CountableInterFilter.countable_sInter_mem S
    ((countable_iff_lt_aleph_one S).mpr hS) a

theorem cardinalInterFilter_aleph_one_iff :
    CardinalInterFilter l (aleph 1) â†” CountableInterFilter l :=
  âŸ¨fun _ â†¦ âŸ¨fun S h a â†¦
    CardinalInterFilter.cardinal_sInter_mem S ((countable_iff_lt_aleph_one S).1 h) aâŸ©,
   fun _ â†¦ CountableInterFilter.toCardinalInterFilter lâŸ©

/-- Every CardinalInterFilter for some c also is a CardinalInterFilter for some a â‰¤ c -/
theorem CardinalInterFilter.of_CardinalInterFilter_of_le (l : Filter Î±) [CardinalInterFilter l c]
    {a : Cardinal.{u}} (hac : a â‰¤ c) :
  CardinalInterFilter l a where
    cardinal_sInter_mem :=
      fun S hS a â†¦ CardinalInterFilter.cardinal_sInter_mem S (lt_of_lt_of_le hS hac) a

theorem CardinalInterFilter.of_CardinalInterFilter_of_lt (l : Filter Î±) [CardinalInterFilter l c]
    {a : Cardinal.{u}} (hac : a < c) : CardinalInterFilter l a :=
  CardinalInterFilter.of_CardinalInterFilter_of_le l (hac.le)

namespace Filter

variable [CardinalInterFilter l c]

theorem cardinal_iInter_mem {s : Î¹ â†’ Set Î±} (hic : #Î¹ < c) :
    (â‹‚ i, s i) âˆˆ l â†” âˆ€ i, s i âˆˆ l := by
  rw [â† sInter_range _]
  apply Iff.trans
  apply cardinal_sInter_mem (lt_of_le_of_lt Cardinal.mk_range_le hic)
  exact forall_mem_range

theorem cardinal_bInter_mem {S : Set Î¹} (hS : #S < c)
    {s : âˆ€ i âˆˆ S, Set Î±} :
    (â‹‚ i, â‹‚ hi : i âˆˆ S, s i â€¹_â€º) âˆˆ l â†” âˆ€ i, âˆ€ hi : i âˆˆ S, s i â€¹_â€º âˆˆ l := by
  rw [biInter_eq_iInter]
  exact (cardinal_iInter_mem hS).trans Subtype.forall

theorem eventually_cardinal_forall {p : Î± â†’ Î¹ â†’ Prop} (hic : #Î¹ < c) :
    (âˆ€á¶  x in l, âˆ€ i, p x i) â†” âˆ€ i, âˆ€á¶  x in l, p x i := by
  simp only [Filter.Eventually, setOf_forall]
  exact cardinal_iInter_mem hic

theorem eventually_cardinal_ball {S : Set Î¹} (hS : #S < c)
    {p : Î± â†’ âˆ€ i âˆˆ S, Prop} :
    (âˆ€á¶  x in l, âˆ€ i hi, p x i hi) â†” âˆ€ i hi, âˆ€á¶  x in l, p x i hi := by
  simp only [Filter.Eventually, setOf_forall]
  exact cardinal_bInter_mem hS

theorem EventuallyLE.cardinal_iUnion {s t : Î¹ â†’ Set Î±} (hic : #Î¹ < c)
    (h : âˆ€ i, s i â‰¤á¶ [l] t i) : â‹ƒ i, s i â‰¤á¶ [l] â‹ƒ i, t i :=
  ((eventually_cardinal_forall hic).2 h).mono fun _ hst hs => mem_iUnion.2 <|
    (mem_iUnion.1 hs).imp hst

theorem EventuallyEq.cardinal_iUnion {s t : Î¹ â†’ Set Î±} (hic : #Î¹ < c)
    (h : âˆ€ i, s i =á¶ [l] t i) : â‹ƒ i, s i =á¶ [l] â‹ƒ i, t i :=
  (EventuallyLE.cardinal_iUnion hic fun i => (h i).le).antisymm
    (EventuallyLE.cardinal_iUnion hic fun i => (h i).symm.le)

theorem EventuallyLE.cardinal_bUnion {S : Set Î¹} (hS : #S < c)
    {s t : âˆ€ i âˆˆ S, Set Î±} (h : âˆ€ i hi, s i hi â‰¤á¶ [l] t i hi) :
    â‹ƒ i âˆˆ S, s i â€¹_â€º â‰¤á¶ [l] â‹ƒ i âˆˆ S, t i â€¹_â€º := by
  simp only [biUnion_eq_iUnion]
  exact EventuallyLE.cardinal_iUnion hS fun i => h i i.2

theorem EventuallyEq.cardinal_bUnion {S : Set Î¹} (hS : #S < c)
    {s t : âˆ€ i âˆˆ S, Set Î±} (h : âˆ€ i hi, s i hi =á¶ [l] t i hi) :
    â‹ƒ i âˆˆ S, s i â€¹_â€º =á¶ [l] â‹ƒ i âˆˆ S, t i â€¹_â€º :=
  (EventuallyLE.cardinal_bUnion hS fun i hi => (h i hi).le).antisymm
    (EventuallyLE.cardinal_bUnion hS fun i hi => (h i hi).symm.le)

theorem EventuallyLE.cardinal_iInter {s t : Î¹ â†’ Set Î±} (hic : #Î¹ < c)
    (h : âˆ€ i, s i â‰¤á¶ [l] t i) : â‹‚ i, s i â‰¤á¶ [l] â‹‚ i, t i :=
  ((eventually_cardinal_forall hic).2 h).mono fun _ hst hs =>
    mem_iInter.2 fun i => hst _ (mem_iInter.1 hs i)

theorem EventuallyEq.cardinal_iInter {s t : Î¹ â†’ Set Î±} (hic : #Î¹ < c)
    (h : âˆ€ i, s i =á¶ [l] t i) : â‹‚ i, s i =á¶ [l] â‹‚ i, t i :=
  (EventuallyLE.cardinal_iInter hic fun i => (h i).le).antisymm
    (EventuallyLE.cardinal_iInter hic fun i => (h i).symm.le)

theorem EventuallyLE.cardinal_bInter {S : Set Î¹} (hS : #S < c)
    {s t : âˆ€ i âˆˆ S, Set Î±} (h : âˆ€ i hi, s i hi â‰¤á¶ [l] t i hi) :
    â‹‚ i âˆˆ S, s i â€¹_â€º â‰¤á¶ [l] â‹‚ i âˆˆ S, t i â€¹_â€º := by
  simp only [biInter_eq_iInter]
  exact EventuallyLE.cardinal_iInter hS fun i => h i i.2

theorem EventuallyEq.cardinal_bInter {S : Set Î¹} (hS : #S < c)
    {s t : âˆ€ i âˆˆ S, Set Î±} (h : âˆ€ i hi, s i hi =á¶ [l] t i hi) :
    â‹‚ i âˆˆ S, s i â€¹_â€º =á¶ [l] â‹‚ i âˆˆ S, t i â€¹_â€º :=
  (EventuallyLE.cardinal_bInter hS fun i hi => (h i hi).le).antisymm
    (EventuallyLE.cardinal_bInter hS fun i hi => (h i hi).symm.le)

lemma Cardinal.mk_insert_le {Î± : Type u} {s : Set Î±} {a : Î±} : #â†‘(insert a s) â‰¤ #â†‘s + 1 := by
  by_cases h : a âˆˆ s
  Â· simp [h]
  Â· rw [Cardinal.mk_insert h]

def ofCardinalInter (l : Set (Set Î±)) (hc : 2 < c)
    (hp : âˆ€ S : Set (Set Î±), (#S < c) â†’ S âŠ† l â†’ â‹‚â‚€ S âˆˆ l)
    (h_mono : âˆ€ s t, s âˆˆ l â†’ s âŠ† t â†’ t âˆˆ l) : Filter Î± where
  sets := l
  univ_sets := by
    apply @sInter_empty Î± â–¸ hp âˆ… (?_) (empty_subset _)
    have this : 0 < c := lt_trans zero_lt_two hc
    rwa [mk_eq_zero]
  sets_of_superset := h_mono _ _
  inter_sets {s t} hs ht := sInter_pair s t â–¸ by
    apply hp _ (?_) (insert_subset_iff.2 âŸ¨hs, singleton_subset_iff.2 htâŸ©)
    have : #({s, t} : Set (Set Î±)) â‰¤ 2 := by
      calc
      _ â‰¤ #({t} : Set (Set Î±)) + 1 := Cardinal.mk_insert_le
      _ = 1 + 1 := by rw [Cardinal.mk_singleton]
      _ = 2 := by norm_num
    exact lt_of_le_of_lt this hc

instance cardinalInter_ofCardinalInter (l : Set (Set Î±)) (hc : 2 < c)
    (hp : âˆ€ S : Set (Set Î±), (#S < c) â†’ S âŠ† l â†’ â‹‚â‚€ S âˆˆ l)
    (h_mono : âˆ€ s t, s âˆˆ l â†’ s âŠ† t â†’ t âˆˆ l) :
    CardinalInterFilter (Filter.ofCardinalInter l hc hp h_mono) c :=
  âŸ¨hpâŸ©

@[simp]
theorem mem_ofCardinalInter {l : Set (Set Î±)} (hc : 2 < c)
    (hp : âˆ€ S : Set (Set Î±), (#S < c) â†’ S âŠ† l â†’ â‹‚â‚€ S âˆˆ l) (h_mono : âˆ€ s t, s âˆˆ l â†’ s âŠ† t â†’ t âˆˆ l)
    {s : Set Î±} : s âˆˆ Filter.ofCardinalInter l hc hp h_mono â†” s âˆˆ l :=
  Iff.rfl

/-- Construct a filter with cardinal `c` intersection property.
Similarly to `Filter.comk`, a set belongs to this filter if its complement satisfies the property.
Similarly to `Filter.ofCardinalInter`,
this constructor deduces some properties from the cardinal `c` intersection property
which becomes the cardinal `c` union property because we take complements of all sets.

Another small difference from `Filter.ofCardinalInter`
is that this definition takes `p : Set Î± â†’ Prop` instead of `Set (Set Î±)`. -/
def ofCardinalUnion (p : Set Î± â†’ Prop) (hc : 2 < c)
    (hUnion : âˆ€ S : Set (Set Î±), (#S < c) â†’ (âˆ€ s âˆˆ S, p s) â†’ p (â‹ƒâ‚€ S))
    (hmono : âˆ€ t, p t â†’ âˆ€ s âŠ† t, p s) : Filter Î± := by
  refine .ofCardinalInter {s | p sá¶œ} hc (fun S hSc hSp â†¦ ?_) fun s t ht hsub â†¦ ?_
  Â· rw [mem_setOf_eq, compl_sInter]
    apply hUnion _ ?_ (forall_mem_image.2 hSp)
    rwa [Cardinal.mk_image_eq compl_injective]
  Â· exact hmono _ ht _ (compl_subset_compl.2 hsub)

instance cardinalInter_ofCardinalUnion (p : Set Î± â†’ Prop) (hc : 2 < c) (hâ‚ hâ‚‚) :
    CardinalInterFilter (Filter.ofCardinalUnion p hc hâ‚ hâ‚‚) c :=
  cardinalInter_ofCardinalInter ..

@[simp]
theorem mem_ofCardinalUnion {p : Set Î± â†’ Prop} (hc : 2 < c) {hunion hmono s} :
    s âˆˆ ofCardinalUnion p hc hunion hmono â†” p sá¶œ :=
  Iff.rfl

instance cardinalInterFilter_principal (s : Set Î±) : CardinalInterFilter (ğ“Ÿ s) c :=
  âŸ¨fun _ _ hS => subset_sInter hSâŸ©

instance cardinalInterFilter_bot : CardinalInterFilter (âŠ¥ : Filter Î±) c := by
  rw [â† principal_empty]
  apply cardinalInterFilter_principal

instance cardinalInterFilter_top : CardinalInterFilter (âŠ¤ : Filter Î±) c := by
  rw [â† principal_univ]
  apply cardinalInterFilter_principal

instance (l : Filter Î²) [CardinalInterFilter l c] (f : Î± â†’ Î²) :
    CardinalInterFilter (comap f l) c := by
  refine' âŸ¨fun S hSc hS => _âŸ©
  choose! t htl ht using hS
  have : (â‹‚ s âˆˆ S, t s) âˆˆ l := (cardinal_bInter_mem hSc).2 htl
  refine' âŸ¨_, this, _âŸ©
  simpa [preimage_iInter] using iInterâ‚‚_mono ht

instance (l : Filter Î±) [CardinalInterFilter l c] (f : Î± â†’ Î²) :
    CardinalInterFilter (map f l) c := by
  refine' âŸ¨fun S hSc hS => _âŸ©
  simp only [mem_map, sInter_eq_biInter, preimage_iInterâ‚‚] at hS âŠ¢
  exact (cardinal_bInter_mem hSc).2 hS

/-- Infimum of two `CardinalInterFilter`s is a `CardinalInterFilter`. This is useful, e.g.,
to automatically get an instance for `residual Î± âŠ“ ğ“Ÿ s`. -/
instance cardinalInterFilter_inf_eq (lâ‚ lâ‚‚ : Filter Î±) [CardinalInterFilter lâ‚ c]
    [CardinalInterFilter lâ‚‚ c] : CardinalInterFilter (lâ‚ âŠ“ lâ‚‚) c := by
  refine' âŸ¨fun S hSc hS => _âŸ©
  choose s hs t ht hst using hS
  replace hs : (â‹‚ i âˆˆ S, s i â€¹_â€º) âˆˆ lâ‚ := (cardinal_bInter_mem hSc).2 hs
  replace ht : (â‹‚ i âˆˆ S, t i â€¹_â€º) âˆˆ lâ‚‚ := (cardinal_bInter_mem hSc).2 ht
  refine' mem_of_superset (inter_mem_inf hs ht) (subset_sInter fun i hi => _)
  rw [hst i hi]
  apply inter_subset_inter <;> exact iInter_subset_of_subset i (iInter_subset _ _)

instance cardinalInterFilter_inf (lâ‚ lâ‚‚ : Filter Î±) {câ‚ câ‚‚ : Cardinal.{u}}
    [CardinalInterFilter lâ‚ câ‚] [CardinalInterFilter lâ‚‚ câ‚‚] : CardinalInterFilter (lâ‚ âŠ“ lâ‚‚)
    (câ‚ âŠ“ câ‚‚) := by
  have : CardinalInterFilter lâ‚ (câ‚ âŠ“ câ‚‚) :=
    CardinalInterFilter.of_CardinalInterFilter_of_le lâ‚ inf_le_left
  have : CardinalInterFilter lâ‚‚ (câ‚ âŠ“ câ‚‚) :=
    CardinalInterFilter.of_CardinalInterFilter_of_le lâ‚‚ inf_le_right
  exact cardinalInterFilter_inf_eq _ _

/-- Supremum of two `CardinalInterFilter`s is a `CardinalInterFilter`. -/
instance cardinalInterFilter_sup_eq (lâ‚ lâ‚‚ : Filter Î±) [CardinalInterFilter lâ‚ c]
    [CardinalInterFilter lâ‚‚ c] : CardinalInterFilter (lâ‚ âŠ” lâ‚‚) c := by
  refine' âŸ¨fun S hSc hS => âŸ¨_, _âŸ©âŸ© <;> refine' (cardinal_sInter_mem hSc).2 fun s hs => _
  exacts [(hS s hs).1, (hS s hs).2]

instance cardinalInterFilter_sup (lâ‚ lâ‚‚ : Filter Î±) {câ‚ câ‚‚ : Cardinal.{u}}
    [CardinalInterFilter lâ‚ câ‚] [CardinalInterFilter lâ‚‚ câ‚‚] :
    CardinalInterFilter (lâ‚ âŠ” lâ‚‚) (câ‚ âŠ“ câ‚‚) := by
  have : CardinalInterFilter lâ‚ (câ‚ âŠ“ câ‚‚) :=
    CardinalInterFilter.of_CardinalInterFilter_of_le lâ‚ inf_le_left
  have : CardinalInterFilter lâ‚‚ (câ‚ âŠ“ câ‚‚) :=
    CardinalInterFilter.of_CardinalInterFilter_of_le lâ‚‚ inf_le_right
  exact cardinalInterFilter_sup_eq _ _

variable (g : Set (Set Î±))

/-- `Filter.CardinalGenerateSets c g` is the (sets of the)
greatest `cardinalInterFilter c` containing `g`.-/
inductive CardinalGenerateSets : Set Î± â†’ Prop
  | basic {s : Set Î±} : s âˆˆ g â†’ CardinalGenerateSets s
  | univ : CardinalGenerateSets univ
  | superset {s t : Set Î±} : CardinalGenerateSets s â†’ s âŠ† t â†’ CardinalGenerateSets t
  | sInter {S : Set (Set Î±)} :
    (#S < c) â†’ (âˆ€ s âˆˆ S, CardinalGenerateSets s) â†’ CardinalGenerateSets (â‹‚â‚€ S)

/-- `Filter.cardinalGenerate c g` is the greatest `cardinalInterFilter c` containing `g`.-/
def cardinalGenerate (hc : 2 < c) : Filter Î± :=
  ofCardinalInter (CardinalGenerateSets g) hc (fun _ => CardinalGenerateSets.sInter) fun _ _ =>
    CardinalGenerateSets.superset

instance (hc : 2 < c) : CardinalInterFilter (cardinalGenerate g hc) c := by
  delta cardinalGenerate; infer_instance

variable {g}

-- Some cardinality related lemmas that I needed
def fin_from_regular (hreg : Cardinal.IsRegular c) (n : â„•) : n < c:= by
  apply lt_of_lt_of_le (nat_lt_aleph0 n) (Cardinal.IsRegular.aleph0_le hreg)

@[simp]
theorem cardinal_iUnion_iff {hÎ¹ : #Î¹ < c} {hreg : Cardinal.IsRegular c}  {t : Î¹ â†’ Set Î±} :
    #(â‹ƒ i, t i) < c â†” âˆ€ i, #(t i) < c := by
  constructor
  Â· exact fun h _ =>  lt_of_le_of_lt (Cardinal.mk_le_mk_of_subset <| subset_iUnion _ _) h
  Â· intro h
    apply lt_of_le_of_lt (Cardinal.mk_sUnion_le _)
    apply Cardinal.mul_lt_of_lt (Cardinal.IsRegular.aleph0_le hreg)
    Â· exact lt_of_le_of_lt Cardinal.mk_range_le hÎ¹
    Â· apply Cardinal.iSup_lt_of_isRegular hreg
      apply lt_of_le_of_lt Cardinal.mk_range_le hÎ¹
      simpa

theorem Cardinal.biUnion_iff {s : Set Î±} {t : âˆ€ a âˆˆ s, Set Î²} {hreg : Cardinal.IsRegular c}
    (hs : #s < c) : #(â‹ƒ a âˆˆ s, t a â€¹_â€º) < c â†” âˆ€ a (ha : a âˆˆ s), # (t a ha) < c := by
  rw [biUnion_eq_iUnion, cardinal_iUnion_iff, SetCoe.forall']
  Â· exact hs
  Â· exact hreg

/-- A set is in the `cardinalInterFilter` generated by `g` if and only if
it contains an intersection of `c` elements of `g`. -/
theorem mem_cardinaleGenerate_iff {s : Set Î±} {hreg : Cardinal.IsRegular c} :
    s âˆˆ cardinalGenerate g (fin_from_regular hreg 2) â†”
    âˆƒ S : Set (Set Î±), S âŠ† g âˆ§ (#S < c) âˆ§ â‹‚â‚€ S âŠ† s := by
  constructor <;> intro h
  Â· induction' h with s hs s t _ st ih S Sct _ ih
    Â· refine âŸ¨{s}, singleton_subset_iff.mpr hs, ?_âŸ©
      norm_num; exact âŸ¨fin_from_regular hreg 1, subset_rflâŸ©
    Â· exact âŸ¨âˆ…, by
        refine âŸ¨empty_subset g, ?_ âŸ©
        constructor
        Â· have : 0 < c := fin_from_regular hreg 0
          rwa [mk_eq_zero]
        Â· simp
        âŸ©
    Â· refine' Exists.imp (fun S => _) ih
      tauto
    choose T Tg Tct hT using ih
    refine' âŸ¨â‹ƒ (s) (H : s âˆˆ S), T s H, by simpa, ?_, _âŸ©
    Â· apply (Cardinal.biUnion_iff Sct).2
      Â· exact Tct
      Â· exact hreg
    Â· apply subset_sInter
      intro s H
      exact subset_trans (sInter_subset_sInter (subset_iUnionâ‚‚ s H)) (hT s H)
  rcases h with âŸ¨S, Sg, Sct, hSâŸ©
  refine' mem_of_superset ((cardinal_sInter_mem Sct).mpr _) hS
  intro s H
  exact CardinalGenerateSets.basic (Sg H)

theorem le_cardinalGenerate_iff_of_cardinalInterFilter {f : Filter Î±} [CardinalInterFilter f c]
    (hc : 2 < c) : f â‰¤ cardinalGenerate g hc â†” g âŠ† f.sets := by
  constructor <;> intro h
  Â· exact subset_trans (fun s => CardinalGenerateSets.basic) h
  intro s hs
  induction' hs with s hs s t _ st ih S Sct _ ih
  Â· exact h hs
  Â· exact univ_mem
  Â· exact mem_of_superset ih st
  exact (cardinal_sInter_mem Sct).mpr ih

/-- `cardinalGenerate g hc` is the greatest `cardinalInterFilter c` containing `g`.-/
theorem cardinalGenerate_isGreatest (hc : 2 < c) :
    IsGreatest { f : Filter Î± | CardinalInterFilter f c âˆ§ g âŠ† f.sets } (cardinalGenerate g hc) := by
  refine' âŸ¨âŸ¨inferInstance, fun s => CardinalGenerateSets.basicâŸ©, _âŸ©
  rintro f âŸ¨fct, hfâŸ©
  rwa [@le_cardinalGenerate_iff_of_cardinalInterFilter _ _ _ fct]

end Filter
