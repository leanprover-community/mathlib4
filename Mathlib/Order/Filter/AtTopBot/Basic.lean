/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Jeremy Avigad, Yury Kudryashov, Patrick Massot
-/
module

public import Mathlib.Order.Filter.Bases.Basic
public import Mathlib.Order.Filter.AtTopBot.Tendsto
public import Mathlib.Order.Nat
public import Mathlib.Tactic.Subsingleton

/-!
# Basic results on `Filter.atTop` and `Filter.atBot` filters

In this file we prove many lemmas like ‚Äúif `f ‚Üí +‚àû`, then `f ¬± c ‚Üí +‚àû`‚Äù.
-/

public section

assert_not_exists Finset

variable {Œπ Œπ' Œ± Œ≤ Œ≥ : Type*}

open Set

namespace Filter

section IsDirected
variable [Preorder Œ±] [IsDirectedOrder Œ±] {p : Œ± ‚Üí Prop}

theorem hasAntitoneBasis_atTop [Nonempty Œ±] : (@atTop Œ± _).HasAntitoneBasis Ici :=
  .iInf_principal fun _ _ ‚Ü¶ Ici_subset_Ici.2

theorem atTop_basis [Nonempty Œ±] : (@atTop Œ± _).HasBasis (fun _ => True) Ici :=
  hasAntitoneBasis_atTop.1

lemma atTop_basis_Ioi [Nonempty Œ±] [NoMaxOrder Œ±] : (@atTop Œ± _).HasBasis (fun _ => True) Ioi :=
  atTop_basis.to_hasBasis (fun a ha => ‚ü®a, ha, Ioi_subset_Ici_self‚ü©) fun a ha =>
    (exists_gt a).imp fun _b hb => ‚ü®ha, Ici_subset_Ioi.2 hb‚ü©

lemma atTop_basis_Ioi' [NoMaxOrder Œ±] (a : Œ±) : atTop.HasBasis (a < ¬∑) Ioi := by
  have : Nonempty Œ± := ‚ü®a‚ü©
  refine atTop_basis_Ioi.to_hasBasis (fun b _ ‚Ü¶ ?_) fun b _ ‚Ü¶ ‚ü®b, trivial, Subset.rfl‚ü©
  obtain ‚ü®c, hac, hbc‚ü© := exists_ge_ge a b
  obtain ‚ü®d, hcd‚ü© := exists_gt c
  exact ‚ü®d, hac.trans_lt hcd, Ioi_subset_Ioi (hbc.trans hcd.le)‚ü©

theorem atTop_basis' (a : Œ±) : atTop.HasBasis (a ‚â§ ¬∑) Ici := by
  have : Nonempty Œ± := ‚ü®a‚ü©
  refine atTop_basis.to_hasBasis (fun b _ ‚Ü¶ ?_) fun b _ ‚Ü¶ ‚ü®b, trivial, Subset.rfl‚ü©
  obtain ‚ü®c, hac, hbc‚ü© := exists_ge_ge a b
  exact ‚ü®c, hac, Ici_subset_Ici.2 hbc‚ü©

variable [Nonempty Œ±]

@[instance]
lemma atTop_neBot : NeBot (atTop : Filter Œ±) := atTop_basis.neBot_iff.2 fun _ => nonempty_Ici

theorem atTop_neBot_iff {Œ± : Type*} [Preorder Œ±] :
    (atTop : Filter Œ±).NeBot ‚Üî Nonempty Œ± ‚àß IsDirectedOrder Œ± := by
  refine ‚ü®fun h ‚Ü¶ ‚ü®nonempty_of_neBot atTop, ‚ü®fun x y ‚Ü¶ ?_‚ü©‚ü©, fun ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ atTop_neBot‚ü©
  exact ((eventually_ge_atTop x).and (eventually_ge_atTop y)).exists

theorem atBot_neBot_iff {Œ± : Type*} [Preorder Œ±] :
    (atBot : Filter Œ±).NeBot ‚Üî Nonempty Œ± ‚àß IsCodirectedOrder Œ± :=
  atTop_neBot_iff (Œ± := Œ±·µí·µà)

@[simp] lemma mem_atTop_sets {s : Set Œ±} : s ‚àà (atTop : Filter Œ±) ‚Üî ‚àÉ a : Œ±, ‚àÄ b ‚â• a, b ‚àà s :=
  atTop_basis.mem_iff.trans <| exists_congr fun _ => iff_of_eq (true_and _)

@[simp] lemma eventually_atTop : (‚àÄ·∂† x in atTop, p x) ‚Üî ‚àÉ a, ‚àÄ b ‚â• a, p b := mem_atTop_sets

theorem frequently_atTop : (‚àÉ·∂† x in atTop, p x) ‚Üî ‚àÄ a, ‚àÉ b ‚â• a, p b :=
  atTop_basis.frequently_iff.trans <| by simp

alias ‚ü®Eventually.exists_forall_of_atTop, _‚ü© := eventually_atTop

lemma exists_eventually_atTop {r : Œ± ‚Üí Œ≤ ‚Üí Prop} :
    (‚àÉ b, ‚àÄ·∂† a in atTop, r a b) ‚Üî ‚àÄ·∂† a‚ÇÄ in atTop, ‚àÉ b, ‚àÄ a ‚â• a‚ÇÄ, r a b := by
  simp_rw [eventually_atTop, ‚Üê exists_swap (Œ± := Œ±)]
  exact exists_congr fun a ‚Ü¶ .symm <| forall_ge_iff <| Monotone.exists fun _ _ _ hb H n hn ‚Ü¶
    H n (hb.trans hn)

theorem map_atTop_eq {f : Œ± ‚Üí Œ≤} : atTop.map f = ‚®Ö a, ùìü (f '' { a' | a ‚â§ a' }) :=
  (atTop_basis.map f).eq_iInf

theorem frequently_atTop' [NoMaxOrder Œ±] : (‚àÉ·∂† x in atTop, p x) ‚Üî ‚àÄ a, ‚àÉ b > a, p b :=
  atTop_basis_Ioi.frequently_iff.trans <| by simp

end IsDirected

section IsCodirected
variable [Preorder Œ±] [IsCodirectedOrder Œ±] {p : Œ± ‚Üí Prop}

lemma atBot_basis_Iio [Nonempty Œ±] [NoMinOrder Œ±] : (@atBot Œ± _).HasBasis (fun _ => True) Iio :=
  atTop_basis_Ioi (Œ± := Œ±·µí·µà)

lemma atBot_basis_Iio' [NoMinOrder Œ±] (a : Œ±) : atBot.HasBasis (¬∑ < a) Iio :=
  atTop_basis_Ioi' (Œ± := Œ±·µí·µà) a

lemma atBot_basis' (a : Œ±) : (@atBot Œ± _).HasBasis (fun x => x ‚â§ a) Iic := atTop_basis' (Œ± := Œ±·µí·µà) _

variable [Nonempty Œ±]

lemma atBot_basis : (@atBot Œ± _).HasBasis (fun _ => True) Iic := atTop_basis (Œ± := Œ±·µí·µà)

@[instance] lemma atBot_neBot : NeBot (atBot : Filter Œ±) := atTop_neBot (Œ± := Œ±·µí·µà)

@[simp] lemma mem_atBot_sets {s : Set Œ±} : s ‚àà (atBot : Filter Œ±) ‚Üî ‚àÉ a : Œ±, ‚àÄ b ‚â§ a, b ‚àà s :=
  mem_atTop_sets (Œ± := Œ±·µí·µà)

@[simp] lemma eventually_atBot : (‚àÄ·∂† x in atBot, p x) ‚Üî ‚àÉ a, ‚àÄ b ‚â§ a, p b := mem_atBot_sets

theorem frequently_atBot : (‚àÉ·∂† x in atBot, p x) ‚Üî ‚àÄ a, ‚àÉ b ‚â§ a, p b := frequently_atTop (Œ± := Œ±·µí·µà)

alias ‚ü®Eventually.exists_forall_of_atBot, _‚ü© := eventually_atBot

lemma exists_eventually_atBot {r : Œ± ‚Üí Œ≤ ‚Üí Prop} :
    (‚àÉ b, ‚àÄ·∂† a in atBot, r a b) ‚Üî ‚àÄ·∂† a‚ÇÄ in atBot, ‚àÉ b, ‚àÄ a ‚â§ a‚ÇÄ, r a b :=
  exists_eventually_atTop (Œ± := Œ±·µí·µà)

theorem map_atBot_eq {f : Œ± ‚Üí Œ≤} : atBot.map f = ‚®Ö a, ùìü (f '' { a' | a' ‚â§ a }) :=
  map_atTop_eq (Œ± := Œ±·µí·µà)

theorem frequently_atBot' [NoMinOrder Œ±] : (‚àÉ·∂† x in atBot, p x) ‚Üî ‚àÄ a, ‚àÉ b < a, p b :=
  frequently_atTop' (Œ± := Œ±·µí·µà)

end IsCodirected

/-!
### Sequences
-/

theorem extraction_of_frequently_atTop {P : ‚Ñï ‚Üí Prop} (h : ‚àÉ·∂† n in atTop, P n) :
    ‚àÉ œÜ : ‚Ñï ‚Üí ‚Ñï, StrictMono œÜ ‚àß ‚àÄ n, P (œÜ n) := by
  rw [frequently_atTop'] at h
  exact Nat.exists_strictMono_subsequence h

theorem extraction_of_eventually_atTop {P : ‚Ñï ‚Üí Prop} (h : ‚àÄ·∂† n in atTop, P n) :
    ‚àÉ œÜ : ‚Ñï ‚Üí ‚Ñï, StrictMono œÜ ‚àß ‚àÄ n, P (œÜ n) :=
  extraction_of_frequently_atTop h.frequently

theorem extraction_forall_of_frequently {P : ‚Ñï ‚Üí ‚Ñï ‚Üí Prop} (h : ‚àÄ n, ‚àÉ·∂† k in atTop, P n k) :
    ‚àÉ œÜ : ‚Ñï ‚Üí ‚Ñï, StrictMono œÜ ‚àß ‚àÄ n, P n (œÜ n) := by
  simp only [frequently_atTop'] at h
  choose u hu hu' using h
  use (fun n => Nat.recOn n (u 0 0) fun n v => u (n + 1) v : ‚Ñï ‚Üí ‚Ñï)
  constructor
  ¬∑ apply strictMono_nat_of_lt_succ
    intro n
    apply hu
  ¬∑ intro n
    cases n <;> simp [hu']

theorem extraction_forall_of_eventually {P : ‚Ñï ‚Üí ‚Ñï ‚Üí Prop} (h : ‚àÄ n, ‚àÄ·∂† k in atTop, P n k) :
    ‚àÉ œÜ : ‚Ñï ‚Üí ‚Ñï, StrictMono œÜ ‚àß ‚àÄ n, P n (œÜ n) :=
  extraction_forall_of_frequently fun n => (h n).frequently

theorem extraction_forall_of_eventually' {P : ‚Ñï ‚Üí ‚Ñï ‚Üí Prop} (h : ‚àÄ n, ‚àÉ N, ‚àÄ k ‚â• N, P n k) :
    ‚àÉ œÜ : ‚Ñï ‚Üí ‚Ñï, StrictMono œÜ ‚àß ‚àÄ n, P n (œÜ n) :=
  extraction_forall_of_eventually (by simp [eventually_atTop, h])

section IsDirected
variable [Preorder Œ±] [IsDirectedOrder Œ±] {F : Filter Œ≤} {u : Œ± ‚Üí Œ≤}

theorem inf_map_atTop_neBot_iff [Nonempty Œ±] :
    NeBot (F ‚äì map u atTop) ‚Üî ‚àÄ U ‚àà F, ‚àÄ N, ‚àÉ n ‚â• N, u n ‚àà U := by
  simp_rw [inf_neBot_iff_frequently_left, frequently_map, frequently_atTop]; rfl

variable [Preorder Œ≤]

lemma exists_le_of_tendsto_atTop (h : Tendsto u atTop atTop) (a : Œ±) (b : Œ≤) :
    ‚àÉ a' ‚â• a, b ‚â§ u a' := by
  have : Nonempty Œ± := ‚ü®a‚ü©
  have : ‚àÄ·∂† x in atTop, a ‚â§ x ‚àß b ‚â§ u x :=
    (eventually_ge_atTop a).and (h.eventually <| eventually_ge_atTop b)
  exact this.exists

theorem exists_le_of_tendsto_atBot (h : Tendsto u atTop atBot) :
    ‚àÄ a b, ‚àÉ a' ‚â• a, u a' ‚â§ b := exists_le_of_tendsto_atTop (Œ≤ := Œ≤·µí·µà) h

theorem exists_lt_of_tendsto_atTop [NoMaxOrder Œ≤] (h : Tendsto u atTop atTop) (a : Œ±) (b : Œ≤) :
    ‚àÉ a' ‚â• a, b < u a' := by
  obtain ‚ü®b', hb'‚ü© := exists_gt b
  rcases exists_le_of_tendsto_atTop h a b' with ‚ü®a', ha', ha''‚ü©
  exact ‚ü®a', ha', lt_of_lt_of_le hb' ha''‚ü©

theorem exists_lt_of_tendsto_atBot [NoMinOrder Œ≤] (h : Tendsto u atTop atBot) :
    ‚àÄ a b, ‚àÉ a' ‚â• a, u a' < b := exists_lt_of_tendsto_atTop (Œ≤ := Œ≤·µí·µà) h

end IsDirected

section IsCodirected
variable [Nonempty Œ±] [Preorder Œ±] [IsCodirectedOrder Œ±] {F : Filter Œ≤} {u : Œ± ‚Üí Œ≤}

theorem inf_map_atBot_neBot_iff : NeBot (F ‚äì map u atBot) ‚Üî ‚àÄ U ‚àà F, ‚àÄ N, ‚àÉ n ‚â§ N, u n ‚àà U :=
  inf_map_atTop_neBot_iff (Œ± := Œ±·µí·µà)

end IsCodirected

section IsDirected
variable [Nonempty Œ±] [Preorder Œ±] [IsDirectedOrder Œ±] {f : Œ± ‚Üí Œ≤} {l : Filter Œ≤}

theorem tendsto_atTop' : Tendsto f atTop l ‚Üî ‚àÄ s ‚àà l, ‚àÉ a, ‚àÄ b ‚â• a, f b ‚àà s := by
  simp only [tendsto_def, mem_atTop_sets, mem_preimage]

theorem tendsto_atTop_principal {s : Set Œ≤} : Tendsto f atTop (ùìü s) ‚Üî ‚àÉ N, ‚àÄ n ‚â• N, f n ‚àà s := by
  simp_rw [tendsto_iff_comap, comap_principal, le_principal_iff, mem_atTop_sets, mem_preimage]

variable [Preorder Œ≤]

/-- A function `f` grows to `+‚àû` independent of an order-preserving embedding `e`. -/
theorem tendsto_atTop_atTop : Tendsto f atTop atTop ‚Üî ‚àÄ b : Œ≤, ‚àÉ i : Œ±, ‚àÄ a : Œ±, i ‚â§ a ‚Üí b ‚â§ f a :=
  tendsto_iInf.trans <| forall_congr' fun _ => tendsto_atTop_principal

theorem tendsto_atTop_atBot : Tendsto f atTop atBot ‚Üî ‚àÄ b : Œ≤, ‚àÉ i : Œ±, ‚àÄ a : Œ±, i ‚â§ a ‚Üí f a ‚â§ b :=
  tendsto_atTop_atTop (Œ≤ := Œ≤·µí·µà)

theorem tendsto_atTop_atTop_iff_of_monotone (hf : Monotone f) :
    Tendsto f atTop atTop ‚Üî ‚àÄ b : Œ≤, ‚àÉ a, b ‚â§ f a :=
  tendsto_atTop_atTop.trans <| forall_congr' fun _ => exists_congr fun a =>
    ‚ü®fun h => h a (le_refl a), fun h _a' ha' => le_trans h <| hf ha'‚ü©

theorem tendsto_atTop_atBot_iff_of_antitone (hf : Antitone f) :
    Tendsto f atTop atBot ‚Üî ‚àÄ b : Œ≤, ‚àÉ a, f a ‚â§ b :=
  tendsto_atTop_atTop_iff_of_monotone (Œ≤ := Œ≤·µí·µà) hf

end IsDirected

section IsCodirected
variable [Nonempty Œ±] [Preorder Œ±] [IsCodirectedOrder Œ±] {f : Œ± ‚Üí Œ≤} {l : Filter Œ≤}

theorem tendsto_atBot' : Tendsto f atBot l ‚Üî ‚àÄ s ‚àà l, ‚àÉ a, ‚àÄ b ‚â§ a, f b ‚àà s :=
  tendsto_atTop' (Œ± := Œ±·µí·µà)

theorem tendsto_atBot_principal {s : Set Œ≤} : Tendsto f atBot (ùìü s) ‚Üî ‚àÉ N, ‚àÄ n ‚â§ N, f n ‚àà s :=
  tendsto_atTop_principal (Œ± := Œ±·µí·µà) (Œ≤ := Œ≤·µí·µà)

variable [Preorder Œ≤]

theorem tendsto_atBot_atTop : Tendsto f atBot atTop ‚Üî ‚àÄ b : Œ≤, ‚àÉ i : Œ±, ‚àÄ a : Œ±, a ‚â§ i ‚Üí b ‚â§ f a :=
  tendsto_atTop_atTop (Œ± := Œ±·µí·µà)

theorem tendsto_atBot_atBot : Tendsto f atBot atBot ‚Üî ‚àÄ b : Œ≤, ‚àÉ i : Œ±, ‚àÄ a : Œ±, a ‚â§ i ‚Üí f a ‚â§ b :=
  tendsto_atTop_atTop (Œ± := Œ±·µí·µà) (Œ≤ := Œ≤·µí·µà)

theorem tendsto_atBot_atBot_iff_of_monotone (hf : Monotone f) :
    Tendsto f atBot atBot ‚Üî ‚àÄ b : Œ≤, ‚àÉ a, f a ‚â§ b :=
  tendsto_atBot_atBot.trans <| forall_congr' fun _ => exists_congr fun a =>
    ‚ü®fun h => h a (le_refl a), fun h _a' ha' => le_trans (hf ha') h‚ü©

theorem tendsto_atBot_atTop_iff_of_antitone (hf : Antitone f) :
    Tendsto f atBot atTop ‚Üî ‚àÄ b : Œ≤, ‚àÉ a, b ‚â§ f a :=
  tendsto_atBot_atBot_iff_of_monotone (Œ≤ := Œ≤·µí·µà) hf

end IsCodirected

alias _root_.Monotone.tendsto_atTop_atTop_iff := tendsto_atTop_atTop_iff_of_monotone

alias _root_.Monotone.tendsto_atBot_atBot_iff := tendsto_atBot_atBot_iff_of_monotone

theorem Tendsto.subseq_mem {F : Filter Œ±} {V : ‚Ñï ‚Üí Set Œ±} (h : ‚àÄ n, V n ‚àà F) {u : ‚Ñï ‚Üí Œ±}
    (hu : Tendsto u atTop F) : ‚àÉ œÜ : ‚Ñï ‚Üí ‚Ñï, StrictMono œÜ ‚àß ‚àÄ n, u (œÜ n) ‚àà V n :=
  extraction_forall_of_eventually'
    (fun n => tendsto_atTop'.mp hu _ (h n) : ‚àÄ n, ‚àÉ N, ‚àÄ k ‚â• N, u k ‚àà V n)

/-- A function `f` maps upwards closed sets (atTop sets) to upwards closed sets when it is a
Galois insertion. The Galois "insertion" and "connection" is weakened to only require it to be an
insertion and a connection above `b`. -/
theorem map_atTop_eq_of_gc_preorder
    [Preorder Œ±] [IsDirectedOrder Œ±] [Preorder Œ≤] [IsDirectedOrder Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : Monotone f) (b : Œ≤)
    (hgi : ‚àÄ c ‚â• b, ‚àÉ x, f x = c ‚àß ‚àÄ a, f a ‚â§ c ‚Üî a ‚â§ x) : map f atTop = atTop := by
  have : Nonempty Œ± := (hgi b le_rfl).nonempty
  choose! g hfg hgle using hgi
  refine le_antisymm (hf.tendsto_atTop_atTop fun c ‚Ü¶ ?_) ?_
  ¬∑ rcases exists_ge_ge c b with ‚ü®d, hcd, hbd‚ü©
    exact ‚ü®g d, hcd.trans (hfg d hbd).ge‚ü©
  ¬∑ have : Nonempty Œ± := ‚ü®g b‚ü©
    rw [(atTop_basis.map f).ge_iff]
    intro a _
    filter_upwards [eventually_ge_atTop (f a), eventually_ge_atTop b] with c hac hbc
    exact ‚ü®g c, (hgle _ hbc _).1 hac, hfg _ hbc‚ü©


/-- A function `f` maps upwards closed sets (atTop sets) to upwards closed sets when it is a
Galois insertion. The Galois "insertion" and "connection" is weakened to only require it to be an
insertion and a connection above `b`. -/
theorem map_atTop_eq_of_gc
    [Preorder Œ±] [IsDirectedOrder Œ±] [PartialOrder Œ≤] [IsDirectedOrder Œ≤]
    {f : Œ± ‚Üí Œ≤} (g : Œ≤ ‚Üí Œ±) (b : Œ≤) (hf : Monotone f)
    (gc : ‚àÄ a, ‚àÄ c ‚â• b, f a ‚â§ c ‚Üî a ‚â§ g c) (hgi : ‚àÄ c ‚â• b, c ‚â§ f (g c)) :
    map f atTop = atTop :=
  map_atTop_eq_of_gc_preorder hf b fun c hc ‚Ü¶
    ‚ü®g c, le_antisymm ((gc _ _ hc).2 le_rfl) (hgi c hc), (gc ¬∑ c hc)‚ü©

theorem map_atBot_eq_of_gc_preorder
    [Preorder Œ±] [IsCodirectedOrder Œ±] [Preorder Œ≤] [IsCodirectedOrder Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : Monotone f) (b : Œ≤)
    (hgi : ‚àÄ c ‚â§ b, ‚àÉ x, f x = c ‚àß ‚àÄ a, c ‚â§ f a ‚Üî x ‚â§ a) : map f atBot = atBot :=
  map_atTop_eq_of_gc_preorder (Œ± := Œ±·µí·µà) (Œ≤ := Œ≤·µí·µà) hf.dual _ hgi

theorem map_atBot_eq_of_gc [Preorder Œ±] [IsCodirectedOrder Œ±]
    [PartialOrder Œ≤] [IsCodirectedOrder Œ≤] {f : Œ± ‚Üí Œ≤} (g : Œ≤ ‚Üí Œ±) (b' : Œ≤)
    (hf : Monotone f) (gc : ‚àÄ a, ‚àÄ b ‚â§ b', b ‚â§ f a ‚Üî g b ‚â§ a) (hgi : ‚àÄ b ‚â§ b', f (g b) ‚â§ b) :
    map f atBot = atBot :=
  map_atTop_eq_of_gc (Œ± := Œ±·µí·µà) (Œ≤ := Œ≤·µí·µà) _ _ hf.dual gc hgi

theorem map_val_atTop_of_Ici_subset [Preorder Œ±] [IsDirectedOrder Œ±] {a : Œ±} {s : Set Œ±}
    (h : Ici a ‚äÜ s) : map ((‚Üë) : s ‚Üí Œ±) atTop = atTop := by
  choose f hl hr using exists_ge_ge (Œ± := Œ±)
  have : DirectedOn (¬∑ ‚â§ ¬∑) s := fun x _ y _ ‚Ü¶
    ‚ü®f a (f x y), h <| hl _ _, (hl x y).trans (hr _ _), (hr x y).trans (hr _ _)‚ü©
  have : IsDirectedOrder s := by
    rw [directedOn_iff_directed] at this
    rwa [IsDirectedOrder, ‚Üê directed_id_iff]
  refine map_atTop_eq_of_gc_preorder (Subtype.mono_coe _) a fun c hc ‚Ü¶ ?_
  exact ‚ü®‚ü®c, h hc‚ü©, rfl, fun _ ‚Ü¶ .rfl‚ü©

@[simp]
theorem _root_.Nat.map_cast_int_atTop : map ((‚Üë) : ‚Ñï ‚Üí ‚Ñ§) atTop = atTop := by
  refine map_atTop_eq_of_gc_preorder (fun _ _ ‚Ü¶ Int.ofNat_le.2) 0 fun n hn ‚Ü¶ ?_
  lift n to ‚Ñï using hn
  exact ‚ü®n, rfl, fun _ ‚Ü¶ Int.ofNat_le‚ü©

/-- The image of the filter `atTop` on `Ici a` under the coercion equals `atTop`. -/
@[simp]
theorem map_val_Ici_atTop [Preorder Œ±] [IsDirectedOrder Œ±] (a : Œ±) :
    map ((‚Üë) : Ici a ‚Üí Œ±) atTop = atTop :=
  map_val_atTop_of_Ici_subset Subset.rfl

/-- The image of the filter `atTop` on `Ioi a` under the coercion equals `atTop`. -/
@[simp]
theorem map_val_Ioi_atTop [Preorder Œ±] [IsDirectedOrder Œ±] [NoMaxOrder Œ±] (a : Œ±) :
    map ((‚Üë) : Ioi a ‚Üí Œ±) atTop = atTop :=
  let ‚ü®_b, hb‚ü© := exists_gt a
  map_val_atTop_of_Ici_subset <| Ici_subset_Ioi.2 hb

/-- The `atTop` filter for an open interval `Ioi a` comes from the `atTop` filter in the ambient
order. -/
theorem atTop_Ioi_eq [Preorder Œ±] [IsDirectedOrder Œ±] (a : Œ±) :
    atTop = comap ((‚Üë) : Ioi a ‚Üí Œ±) atTop := by
  rcases isEmpty_or_nonempty (Ioi a) with h | ‚ü®‚ü®b, hb‚ü©‚ü©
  ¬∑ subsingleton
  ¬∑ rw [‚Üê map_val_atTop_of_Ici_subset (Ici_subset_Ioi.2 hb), comap_map Subtype.coe_injective]

/-- The `atTop` filter for an open interval `Ici a` comes from the `atTop` filter in the ambient
order. -/
theorem atTop_Ici_eq [Preorder Œ±] [IsDirectedOrder Œ±] (a : Œ±) :
    atTop = comap ((‚Üë) : Ici a ‚Üí Œ±) atTop := by
  rw [‚Üê map_val_Ici_atTop a, comap_map Subtype.coe_injective]

/-- The `atBot` filter for an open interval `Iio a` comes from the `atBot` filter in the ambient
order. -/
@[simp]
theorem map_val_Iio_atBot [Preorder Œ±] [IsCodirectedOrder Œ±] [NoMinOrder Œ±] (a : Œ±) :
    map ((‚Üë) : Iio a ‚Üí Œ±) atBot = atBot :=
  map_val_Ioi_atTop (OrderDual.toDual a)

/-- The `atBot` filter for an open interval `Iio a` comes from the `atBot` filter in the ambient
order. -/
theorem atBot_Iio_eq [Preorder Œ±] [IsCodirectedOrder Œ±] (a : Œ±) :
    atBot = comap ((‚Üë) : Iio a ‚Üí Œ±) atBot :=
  atTop_Ioi_eq (OrderDual.toDual a)

/-- The `atBot` filter for an open interval `Iic a` comes from the `atBot` filter in the ambient
order. -/
@[simp]
theorem map_val_Iic_atBot [Preorder Œ±] [IsCodirectedOrder Œ±] (a : Œ±) :
    map ((‚Üë) : Iic a ‚Üí Œ±) atBot = atBot :=
  map_val_Ici_atTop (OrderDual.toDual a)

/-- The `atBot` filter for an open interval `Iic a` comes from the `atBot` filter in the ambient
order. -/
theorem atBot_Iic_eq [Preorder Œ±] [IsCodirectedOrder Œ±] (a : Œ±) :
    atBot = comap ((‚Üë) : Iic a ‚Üí Œ±) atBot :=
  atTop_Ici_eq (OrderDual.toDual a)

theorem tendsto_Ioi_atTop [Preorder Œ±] [IsDirectedOrder Œ±]
    {a : Œ±} {f : Œ≤ ‚Üí Ioi a} {l : Filter Œ≤} :
    Tendsto f l atTop ‚Üî Tendsto (fun x => (f x : Œ±)) l atTop := by
  rw [atTop_Ioi_eq, tendsto_comap_iff, Function.comp_def]

theorem tendsto_Iio_atBot [Preorder Œ±] [IsCodirectedOrder Œ±]
    {a : Œ±} {f : Œ≤ ‚Üí Iio a} {l : Filter Œ≤} :
    Tendsto f l atBot ‚Üî Tendsto (fun x => (f x : Œ±)) l atBot :=
  tendsto_Ioi_atTop (Œ± := Œ±·µí·µà)

theorem tendsto_Ici_atTop [Preorder Œ±] [IsDirectedOrder Œ±]
    {a : Œ±} {f : Œ≤ ‚Üí Ici a} {l : Filter Œ≤} :
    Tendsto f l atTop ‚Üî Tendsto (fun x => (f x : Œ±)) l atTop := by
  rw [atTop_Ici_eq, tendsto_comap_iff, Function.comp_def]

theorem tendsto_Iic_atBot [Preorder Œ±] [IsCodirectedOrder Œ±]
    {a : Œ±} {f : Œ≤ ‚Üí Iic a} {l : Filter Œ≤} :
    Tendsto f l atBot ‚Üî Tendsto (fun x => (f x : Œ±)) l atBot :=
  tendsto_Ici_atTop (Œ± := Œ±·µí·µà)

@[simp]
theorem tendsto_comp_val_Ioi_atTop [Preorder Œ±] [IsDirectedOrder Œ±] [NoMaxOrder Œ±]
    {a : Œ±} {f : Œ± ‚Üí Œ≤} {l : Filter Œ≤} :
    Tendsto (fun x : Ioi a => f x) atTop l ‚Üî Tendsto f atTop l := by
  rw [‚Üê map_val_Ioi_atTop a, tendsto_map'_iff, Function.comp_def]

@[simp]
theorem tendsto_comp_val_Ici_atTop [Preorder Œ±] [IsDirectedOrder Œ±]
    {a : Œ±} {f : Œ± ‚Üí Œ≤} {l : Filter Œ≤} :
    Tendsto (fun x : Ici a => f x) atTop l ‚Üî Tendsto f atTop l := by
  rw [‚Üê map_val_Ici_atTop a, tendsto_map'_iff, Function.comp_def]

@[simp]
theorem tendsto_comp_val_Iio_atBot [Preorder Œ±] [IsCodirectedOrder Œ±] [NoMinOrder Œ±]
    {a : Œ±} {f : Œ± ‚Üí Œ≤} {l : Filter Œ≤} :
    Tendsto (fun x : Iio a => f x) atBot l ‚Üî Tendsto f atBot l :=
  tendsto_comp_val_Ioi_atTop (Œ± := Œ±·µí·µà)

@[simp]
theorem tendsto_comp_val_Iic_atBot [Preorder Œ±] [IsCodirectedOrder Œ±]
    {a : Œ±} {f : Œ± ‚Üí Œ≤} {l : Filter Œ≤} :
    Tendsto (fun x : Iic a => f x) atBot l ‚Üî Tendsto f atBot l :=
  tendsto_comp_val_Ici_atTop (Œ± := Œ±·µí·µà)

theorem map_add_atTop_eq_nat (k : ‚Ñï) : map (fun a => a + k) atTop = atTop :=
  map_atTop_eq_of_gc (¬∑ - k) k (fun _ _ h => Nat.add_le_add_right h k)
    (fun _ _ h => (Nat.le_sub_iff_add_le h).symm) fun a h => by rw [Nat.sub_add_cancel h]

theorem map_sub_atTop_eq_nat (k : ‚Ñï) : map (fun a => a - k) atTop = atTop :=
  map_atTop_eq_of_gc (¬∑ + k) 0 (fun _ _ h => Nat.sub_le_sub_right h _)
    (fun _ _ _ => Nat.sub_le_iff_le_add) fun b _ => by rw [Nat.add_sub_cancel_right]

theorem tendsto_add_atTop_nat (k : ‚Ñï) : Tendsto (fun a => a + k) atTop atTop :=
  le_of_eq (map_add_atTop_eq_nat k)

theorem tendsto_sub_atTop_nat (k : ‚Ñï) : Tendsto (fun a => a - k) atTop atTop :=
  le_of_eq (map_sub_atTop_eq_nat k)

theorem tendsto_add_atTop_iff_nat {f : ‚Ñï ‚Üí Œ±} {l : Filter Œ±} (k : ‚Ñï) :
    Tendsto (fun n => f (n + k)) atTop l ‚Üî Tendsto f atTop l :=
  show Tendsto (f ‚àò fun n => n + k) atTop l ‚Üî Tendsto f atTop l by
    rw [‚Üê tendsto_map'_iff, map_add_atTop_eq_nat]

theorem map_div_atTop_eq_nat (k : ‚Ñï) (hk : 0 < k) : map (fun a => a / k) atTop = atTop :=
  map_atTop_eq_of_gc (fun b => k * b + (k - 1)) 1 (fun _ _ h => Nat.div_le_div_right h)
    (fun a b _ => by rw [Nat.div_le_iff_le_mul_add_pred hk])
    fun b _ => by rw [Nat.mul_add_div hk, Nat.div_eq_of_lt, Nat.add_zero]; lia

section NeBot
variable [Preorder Œ≤] {l : Filter Œ±} [NeBot l] {f : Œ± ‚Üí Œ≤}

theorem not_bddAbove_of_tendsto_atTop [NoMaxOrder Œ≤] (h : Tendsto f l atTop) :
    ¬¨BddAbove (range f) := by
  rintro ‚ü®M, hM‚ü©
  have : ‚àÄ x, f x ‚â§ M := by aesop
  have : ‚àÖ = f ‚Åª¬π' Ioi M := by aesop (add forward safe not_le_of_gt)
  apply Filter.empty_notMem l
  aesop (add safe Ioi_mem_atTop)

theorem not_bddBelow_of_tendsto_atBot [NoMinOrder Œ≤] (h : Tendsto f l atBot) :
    ¬¨BddBelow (range f) := not_bddAbove_of_tendsto_atTop (Œ≤ := Œ≤·µí·µà) h

end NeBot

theorem HasAntitoneBasis.eventually_subset [Preorder Œπ] {l : Filter Œ±} {s : Œπ ‚Üí Set Œ±}
    (hl : l.HasAntitoneBasis s) {t : Set Œ±} (ht : t ‚àà l) : ‚àÄ·∂† i in atTop, s i ‚äÜ t :=
  let ‚ü®i, _, hi‚ü© := hl.1.mem_iff.1 ht
  (eventually_ge_atTop i).mono fun _j hj => (hl.antitone hj).trans hi

protected theorem HasAntitoneBasis.tendsto [Preorder Œπ] {l : Filter Œ±} {s : Œπ ‚Üí Set Œ±}
    (hl : l.HasAntitoneBasis s) {œÜ : Œπ ‚Üí Œ±} (h : ‚àÄ i : Œπ, œÜ i ‚àà s i) : Tendsto œÜ atTop l :=
  fun _t ht => mem_map.2 <| (hl.eventually_subset ht).mono fun i hi => hi (h i)

theorem HasAntitoneBasis.comp_mono [Nonempty Œπ] [Preorder Œπ] [IsDirectedOrder Œπ] [Preorder Œπ']
    {l : Filter Œ±}
    {s : Œπ' ‚Üí Set Œ±} (hs : l.HasAntitoneBasis s) {œÜ : Œπ ‚Üí Œπ'} (œÜ_mono : Monotone œÜ)
    (hœÜ : Tendsto œÜ atTop atTop) : l.HasAntitoneBasis (s ‚àò œÜ) :=
  ‚ü®hs.1.to_hasBasis
      (fun n _ => (hœÜ.eventually_ge_atTop n).exists.imp fun _m hm => ‚ü®trivial, hs.antitone hm‚ü©)
      fun n _ => ‚ü®œÜ n, trivial, Subset.rfl‚ü©,
    hs.antitone.comp_monotone œÜ_mono‚ü©

theorem HasAntitoneBasis.comp_strictMono {l : Filter Œ±} {s : ‚Ñï ‚Üí Set Œ±} (hs : l.HasAntitoneBasis s)
    {œÜ : ‚Ñï ‚Üí ‚Ñï} (hœÜ : StrictMono œÜ) : l.HasAntitoneBasis (s ‚àò œÜ) :=
  hs.comp_mono hœÜ.monotone hœÜ.tendsto_atTop

theorem subseq_forall_of_frequently {Œπ : Type*} {x : ‚Ñï ‚Üí Œπ} {p : Œπ ‚Üí Prop} {l : Filter Œπ}
    (h_tendsto : Tendsto x atTop l) (h : ‚àÉ·∂† n in atTop, p (x n)) :
    ‚àÉ ns : ‚Ñï ‚Üí ‚Ñï, Tendsto (fun n => x (ns n)) atTop l ‚àß ‚àÄ n, p (x (ns n)) := by
  choose ns hge hns using frequently_atTop.1 h
  exact ‚ü®ns, h_tendsto.comp (tendsto_atTop_mono hge tendsto_id), hns‚ü©

end Filter
