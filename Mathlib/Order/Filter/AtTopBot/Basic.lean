/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Jeremy Avigad, Yury Kudryashov, Patrick Massot
-/
import Mathlib.Order.Filter.Bases
import Mathlib.Data.Set.Finite.Lemmas
import Mathlib.Order.Filter.AtTopBot.Prod

/-!
# Basic results on `Filter.atTop` and `Filter.atBot` filters

In this file we prove many lemmas like â€œif `f â†’ +âˆ`, then `f Â± c â†’ +âˆ`â€.
-/

variable {Î¹ Î¹' Î± Î² Î³ : Type*}

open Set

namespace Filter

theorem eventually_forall_ge_atTop [Preorder Î±] {p : Î± â†’ Prop} :
    (âˆ€á¶  x in atTop, âˆ€ y, x â‰¤ y â†’ p y) â†” âˆ€á¶  x in atTop, p x := by
  refine âŸ¨fun h â†¦ h.mono fun x hx â†¦ hx x le_rfl, fun h â†¦ ?_âŸ©
  rcases (hasBasis_iInf_principal_finite _).eventually_iff.1 h with âŸ¨S, hSf, hSâŸ©
  refine mem_iInf_of_iInter hSf (V := fun x â†¦ Ici x.1) (fun _ â†¦ Subset.rfl) fun x hx y hy â†¦ ?_
  simp only [mem_iInter] at hS hx
  exact hS fun z hz â†¦ le_trans (hx âŸ¨z, hzâŸ©) hy

theorem eventually_forall_le_atBot [Preorder Î±] {p : Î± â†’ Prop} :
    (âˆ€á¶  x in atBot, âˆ€ y, y â‰¤ x â†’ p y) â†” âˆ€á¶  x in atBot, p x :=
  eventually_forall_ge_atTop (Î± := Î±áµ’áµˆ)

theorem Tendsto.eventually_forall_ge_atTop [Preorder Î²] {l : Filter Î±}
    {p : Î² â†’ Prop} {f : Î± â†’ Î²} (hf : Tendsto f l atTop) (h_evtl : âˆ€á¶  x in atTop, p x) :
    âˆ€á¶  x in l, âˆ€ y, f x â‰¤ y â†’ p y := by
  rw [â† Filter.eventually_forall_ge_atTop] at h_evtl; exact (h_evtl.comap f).filter_mono hf.le_comap

theorem Tendsto.eventually_forall_le_atBot [Preorder Î²] {l : Filter Î±}
    {p : Î² â†’ Prop} {f : Î± â†’ Î²} (hf : Tendsto f l atBot) (h_evtl : âˆ€á¶  x in atBot, p x) :
    âˆ€á¶  x in l, âˆ€ y, y â‰¤ f x â†’ p y := by
  rw [â† Filter.eventually_forall_le_atBot] at h_evtl; exact (h_evtl.comap f).filter_mono hf.le_comap

section IsDirected
variable [Preorder Î±] [IsDirected Î± (Â· â‰¤ Â·)] {p : Î± â†’ Prop}

theorem hasAntitoneBasis_atTop [Nonempty Î±] : (@atTop Î± _).HasAntitoneBasis Ici :=
  .iInf_principal fun _ _ â†¦ Ici_subset_Ici.2

theorem atTop_basis [Nonempty Î±] : (@atTop Î± _).HasBasis (fun _ => True) Ici :=
  hasAntitoneBasis_atTop.1

lemma atTop_basis_Ioi [Nonempty Î±] [NoMaxOrder Î±] : (@atTop Î± _).HasBasis (fun _ => True) Ioi :=
  atTop_basis.to_hasBasis (fun a ha => âŸ¨a, ha, Ioi_subset_Ici_selfâŸ©) fun a ha =>
    (exists_gt a).imp fun _b hb => âŸ¨ha, Ici_subset_Ioi.2 hbâŸ©

lemma atTop_basis_Ioi' [NoMaxOrder Î±] (a : Î±) : atTop.HasBasis (a < Â·) Ioi := by
  have : Nonempty Î± := âŸ¨aâŸ©
  refine atTop_basis_Ioi.to_hasBasis (fun b _ â†¦ ?_) fun b _ â†¦ âŸ¨b, trivial, Subset.rflâŸ©
  obtain âŸ¨c, hac, hbcâŸ© := exists_ge_ge a b
  obtain âŸ¨d, hcdâŸ© := exists_gt c
  exact âŸ¨d, hac.trans_lt hcd, Ioi_subset_Ioi (hbc.trans hcd.le)âŸ©

theorem atTop_basis' (a : Î±) : atTop.HasBasis (a â‰¤ Â·) Ici := by
  have : Nonempty Î± := âŸ¨aâŸ©
  refine atTop_basis.to_hasBasis (fun b _ â†¦ ?_) fun b _ â†¦ âŸ¨b, trivial, Subset.rflâŸ©
  obtain âŸ¨c, hac, hbcâŸ© := exists_ge_ge a b
  exact âŸ¨c, hac, Ici_subset_Ici.2 hbcâŸ©

variable [Nonempty Î±]

@[instance]
lemma atTop_neBot : NeBot (atTop : Filter Î±) := atTop_basis.neBot_iff.2 fun _ => nonempty_Ici

theorem atTop_neBot_iff {Î± : Type*} [Preorder Î±] :
    (atTop : Filter Î±).NeBot â†” Nonempty Î± âˆ§ IsDirected Î± (Â· â‰¤ Â·) := by
  refine âŸ¨fun h â†¦ âŸ¨nonempty_of_neBot atTop, âŸ¨fun x y â†¦ ?_âŸ©âŸ©, fun âŸ¨hâ‚, hâ‚‚âŸ© â†¦ atTop_neBotâŸ©
  exact ((eventually_ge_atTop x).and (eventually_ge_atTop y)).exists

theorem atBot_neBot_iff {Î± : Type*} [Preorder Î±] :
    (atBot : Filter Î±).NeBot â†” Nonempty Î± âˆ§ IsDirected Î± (Â· â‰¥ Â·) :=
  atTop_neBot_iff (Î± := Î±áµ’áµˆ)

@[simp] lemma mem_atTop_sets {s : Set Î±} : s âˆˆ (atTop : Filter Î±) â†” âˆƒ a : Î±, âˆ€ b â‰¥ a, b âˆˆ s :=
  atTop_basis.mem_iff.trans <| exists_congr fun _ => iff_of_eq (true_and _)

@[simp] lemma eventually_atTop : (âˆ€á¶  x in atTop, p x) â†” âˆƒ a, âˆ€ b â‰¥ a, p b := mem_atTop_sets

theorem frequently_atTop : (âˆƒá¶  x in atTop, p x) â†” âˆ€ a, âˆƒ b â‰¥ a, p b :=
  atTop_basis.frequently_iff.trans <| by simp

alias âŸ¨Eventually.exists_forall_of_atTop, _âŸ© := eventually_atTop

lemma exists_eventually_atTop {r : Î± â†’ Î² â†’ Prop} :
    (âˆƒ b, âˆ€á¶  a in atTop, r a b) â†” âˆ€á¶  aâ‚€ in atTop, âˆƒ b, âˆ€ a â‰¥ aâ‚€, r a b := by
  simp_rw [eventually_atTop, â† exists_swap (Î± := Î±)]
  exact exists_congr fun a â†¦ .symm <| forall_ge_iff <| Monotone.exists fun _ _ _ hb H n hn â†¦
    H n (hb.trans hn)

theorem map_atTop_eq {f : Î± â†’ Î²} : atTop.map f = â¨… a, ğ“Ÿ (f '' { a' | a â‰¤ a' }) :=
  (atTop_basis.map f).eq_iInf

theorem frequently_atTop' [NoMaxOrder Î±] : (âˆƒá¶  x in atTop, p x) â†” âˆ€ a, âˆƒ b > a, p b :=
  atTop_basis_Ioi.frequently_iff.trans <| by simp

end IsDirected

section IsCodirected
variable [Preorder Î±] [IsDirected Î± (Â· â‰¥ Â·)] {p : Î± â†’ Prop}

lemma atBot_basis_Iio [Nonempty Î±] [NoMinOrder Î±] : (@atBot Î± _).HasBasis (fun _ => True) Iio :=
  atTop_basis_Ioi (Î± := Î±áµ’áµˆ)

lemma atBot_basis_Iio' [NoMinOrder Î±] (a : Î±) : atBot.HasBasis (Â· < a) Iio :=
  atTop_basis_Ioi' (Î± := Î±áµ’áµˆ) a

lemma atBot_basis' (a : Î±) : (@atBot Î± _).HasBasis (fun x => x â‰¤ a) Iic := atTop_basis' (Î± := Î±áµ’áµˆ) _

variable [Nonempty Î±]

lemma atBot_basis : (@atBot Î± _).HasBasis (fun _ => True) Iic := atTop_basis (Î± := Î±áµ’áµˆ)

@[instance] lemma atBot_neBot : NeBot (atBot : Filter Î±) := atTop_neBot (Î± := Î±áµ’áµˆ)

@[simp] lemma mem_atBot_sets {s : Set Î±} : s âˆˆ (atBot : Filter Î±) â†” âˆƒ a : Î±, âˆ€ b â‰¤ a, b âˆˆ s :=
  mem_atTop_sets (Î± := Î±áµ’áµˆ)

@[simp] lemma eventually_atBot : (âˆ€á¶  x in atBot, p x) â†” âˆƒ a, âˆ€ b â‰¤ a, p b := mem_atBot_sets

theorem frequently_atBot : (âˆƒá¶  x in atBot, p x) â†” âˆ€ a, âˆƒ b â‰¤ a, p b := frequently_atTop (Î± := Î±áµ’áµˆ)

alias âŸ¨Eventually.exists_forall_of_atBot, _âŸ© := eventually_atBot

lemma exists_eventually_atBot {r : Î± â†’ Î² â†’ Prop} :
    (âˆƒ b, âˆ€á¶  a in atBot, r a b) â†” âˆ€á¶  aâ‚€ in atBot, âˆƒ b, âˆ€ a â‰¤ aâ‚€, r a b :=
  exists_eventually_atTop (Î± := Î±áµ’áµˆ)

theorem map_atBot_eq {f : Î± â†’ Î²} : atBot.map f = â¨… a, ğ“Ÿ (f '' { a' | a' â‰¤ a }) :=
  map_atTop_eq (Î± := Î±áµ’áµˆ)

theorem frequently_atBot' [NoMinOrder Î±] : (âˆƒá¶  x in atBot, p x) â†” âˆ€ a, âˆƒ b < a, p b :=
  frequently_atTop' (Î± := Î±áµ’áµˆ)

end IsCodirected

/-!
### Sequences
-/

theorem extraction_of_frequently_atTop' {P : â„• â†’ Prop} (h : âˆ€ N, âˆƒ n > N, P n) :
    âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ âˆ€ n, P (Ï† n) := by
  choose u hu hu' using h
  refine âŸ¨fun n => u^[n + 1] 0, strictMono_nat_of_lt_succ fun n => ?_, fun n => ?_âŸ©
  Â· exact Trans.trans (hu _) (Function.iterate_succ_apply' _ _ _).symm
  Â· simpa only [Function.iterate_succ_apply'] using hu' _

theorem extraction_of_frequently_atTop {P : â„• â†’ Prop} (h : âˆƒá¶  n in atTop, P n) :
    âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ âˆ€ n, P (Ï† n) := by
  rw [frequently_atTop'] at h
  exact extraction_of_frequently_atTop' h

theorem extraction_of_eventually_atTop {P : â„• â†’ Prop} (h : âˆ€á¶  n in atTop, P n) :
    âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ âˆ€ n, P (Ï† n) :=
  extraction_of_frequently_atTop h.frequently

theorem extraction_forall_of_frequently {P : â„• â†’ â„• â†’ Prop} (h : âˆ€ n, âˆƒá¶  k in atTop, P n k) :
    âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ âˆ€ n, P n (Ï† n) := by
  simp only [frequently_atTop'] at h
  choose u hu hu' using h
  use (fun n => Nat.recOn n (u 0 0) fun n v => u (n + 1) v : â„• â†’ â„•)
  constructor
  Â· apply strictMono_nat_of_lt_succ
    intro n
    apply hu
  Â· intro n
    cases n <;> simp [hu']

theorem extraction_forall_of_eventually {P : â„• â†’ â„• â†’ Prop} (h : âˆ€ n, âˆ€á¶  k in atTop, P n k) :
    âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ âˆ€ n, P n (Ï† n) :=
  extraction_forall_of_frequently fun n => (h n).frequently

theorem extraction_forall_of_eventually' {P : â„• â†’ â„• â†’ Prop} (h : âˆ€ n, âˆƒ N, âˆ€ k â‰¥ N, P n k) :
    âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ âˆ€ n, P n (Ï† n) :=
  extraction_forall_of_eventually (by simp [eventually_atTop, h])

theorem Eventually.atTop_of_arithmetic {p : â„• â†’ Prop} {n : â„•} (hn : n â‰  0)
    (hp : âˆ€ k < n, âˆ€á¶  a in atTop, p (n * a + k)) : âˆ€á¶  a in atTop, p a := by
  simp only [eventually_atTop] at hp âŠ¢
  choose! N hN using hp
  refine âŸ¨(Finset.range n).sup (n * N Â·), fun b hb => ?_âŸ©
  rw [â† Nat.div_add_mod b n]
  have hlt := Nat.mod_lt b hn.bot_lt
  refine hN _ hlt _ ?_
  rw [ge_iff_le, Nat.le_div_iff_mul_le hn.bot_lt, mul_comm]
  exact (Finset.le_sup (f := (n * N Â·)) (Finset.mem_range.2 hlt)).trans hb

section IsDirected
variable [Preorder Î±] [IsDirected Î± (Â· â‰¤ Â·)] {F : Filter Î²} {u : Î± â†’ Î²}

theorem inf_map_atTop_neBot_iff [Nonempty Î±] :
    NeBot (F âŠ“ map u atTop) â†” âˆ€ U âˆˆ F, âˆ€ N, âˆƒ n â‰¥ N, u n âˆˆ U := by
  simp_rw [inf_neBot_iff_frequently_left, frequently_map, frequently_atTop]; rfl

variable [Preorder Î²]

lemma exists_le_of_tendsto_atTop (h : Tendsto u atTop atTop) (a : Î±) (b : Î²) :
    âˆƒ a' â‰¥ a, b â‰¤ u a' := by
  have : Nonempty Î± := âŸ¨aâŸ©
  have : âˆ€á¶  x in atTop, a â‰¤ x âˆ§ b â‰¤ u x :=
    (eventually_ge_atTop a).and (h.eventually <| eventually_ge_atTop b)
  exact this.exists

-- @[nolint ge_or_gt] -- Porting note: restore attribute
theorem exists_le_of_tendsto_atBot (h : Tendsto u atTop atBot) :
    âˆ€ a b, âˆƒ a' â‰¥ a, u a' â‰¤ b := exists_le_of_tendsto_atTop (Î² := Î²áµ’áµˆ) h

theorem exists_lt_of_tendsto_atTop [NoMaxOrder Î²] (h : Tendsto u atTop atTop) (a : Î±) (b : Î²) :
    âˆƒ a' â‰¥ a, b < u a' := by
  obtain âŸ¨b', hb'âŸ© := exists_gt b
  rcases exists_le_of_tendsto_atTop h a b' with âŸ¨a', ha', ha''âŸ©
  exact âŸ¨a', ha', lt_of_lt_of_le hb' ha''âŸ©

-- @[nolint ge_or_gt] -- Porting note: restore attribute
theorem exists_lt_of_tendsto_atBot [NoMinOrder Î²] (h : Tendsto u atTop atBot) :
    âˆ€ a b, âˆƒ a' â‰¥ a, u a' < b := exists_lt_of_tendsto_atTop (Î² := Î²áµ’áµˆ) h

end IsDirected

section IsCodirected
variable [Nonempty Î±] [Preorder Î±] [IsDirected Î± (Â· â‰¥ Â·)] {F : Filter Î²} {u : Î± â†’ Î²}

theorem inf_map_atBot_neBot_iff : NeBot (F âŠ“ map u atBot) â†” âˆ€ U âˆˆ F, âˆ€ N, âˆƒ n â‰¤ N, u n âˆˆ U :=
  inf_map_atTop_neBot_iff (Î± := Î±áµ’áµˆ)

end IsCodirected

/-- If `u` is a sequence which is unbounded above,
then after any point, it reaches a value strictly greater than all previous values.
-/
theorem high_scores [LinearOrder Î²] [NoMaxOrder Î²] {u : â„• â†’ Î²} (hu : Tendsto u atTop atTop) :
    âˆ€ N, âˆƒ n â‰¥ N, âˆ€ k < n, u k < u n := by
  intro N
  obtain âŸ¨k : â„•, - : k â‰¤ N, hku : âˆ€ l â‰¤ N, u l â‰¤ u kâŸ© : âˆƒ k â‰¤ N, âˆ€ l â‰¤ N, u l â‰¤ u k :=
    exists_max_image _ u (finite_le_nat N) âŸ¨N, le_refl NâŸ©
  have ex : âˆƒ n â‰¥ N, u k < u n := exists_lt_of_tendsto_atTop hu _ _
  obtain âŸ¨n : â„•, hnN : n â‰¥ N, hnk : u k < u n, hn_min : âˆ€ m, m < n â†’ N â‰¤ m â†’ u m â‰¤ u kâŸ© :
      âˆƒ n â‰¥ N, u k < u n âˆ§ âˆ€ m, m < n â†’ N â‰¤ m â†’ u m â‰¤ u k := by
    rcases Nat.findX ex with âŸ¨n, âŸ¨hnN, hnkâŸ©, hn_minâŸ©
    push_neg at hn_min
    exact âŸ¨n, hnN, hnk, hn_minâŸ©
  use n, hnN
  rintro (l : â„•) (hl : l < n)
  have hlk : u l â‰¤ u k := by
    rcases (le_total l N : l â‰¤ N âˆ¨ N â‰¤ l) with H | H
    Â· exact hku l H
    Â· exact hn_min l hl H
  calc
    u l â‰¤ u k := hlk
    _ < u n := hnk

-- see Note [nolint_ge]
/-- If `u` is a sequence which is unbounded below,
then after any point, it reaches a value strictly smaller than all previous values.
-/
-- @[nolint ge_or_gt] Porting note: restore attribute
theorem low_scores [LinearOrder Î²] [NoMinOrder Î²] {u : â„• â†’ Î²} (hu : Tendsto u atTop atBot) :
    âˆ€ N, âˆƒ n â‰¥ N, âˆ€ k < n, u n < u k :=
  @high_scores Î²áµ’áµˆ _ _ _ hu

/-- If `u` is a sequence which is unbounded above,
then it `Frequently` reaches a value strictly greater than all previous values.
-/
theorem frequently_high_scores [LinearOrder Î²] [NoMaxOrder Î²] {u : â„• â†’ Î²}
    (hu : Tendsto u atTop atTop) : âˆƒá¶  n in atTop, âˆ€ k < n, u k < u n := by
  simpa [frequently_atTop] using high_scores hu

/-- If `u` is a sequence which is unbounded below,
then it `Frequently` reaches a value strictly smaller than all previous values.
-/
theorem frequently_low_scores [LinearOrder Î²] [NoMinOrder Î²] {u : â„• â†’ Î²}
    (hu : Tendsto u atTop atBot) : âˆƒá¶  n in atTop, âˆ€ k < n, u n < u k :=
  @frequently_high_scores Î²áµ’áµˆ _ _ _ hu

theorem strictMono_subseq_of_tendsto_atTop [LinearOrder Î²] [NoMaxOrder Î²] {u : â„• â†’ Î²}
    (hu : Tendsto u atTop atTop) : âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ StrictMono (u âˆ˜ Ï†) :=
  let âŸ¨Ï†, h, h'âŸ© := extraction_of_frequently_atTop (frequently_high_scores hu)
  âŸ¨Ï†, h, fun _ m hnm => h' m _ (h hnm)âŸ©

theorem strictMono_subseq_of_id_le {u : â„• â†’ â„•} (hu : âˆ€ n, n â‰¤ u n) :
    âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ StrictMono (u âˆ˜ Ï†) :=
  strictMono_subseq_of_tendsto_atTop (tendsto_atTop_mono hu tendsto_id)

section IsDirected
variable [Nonempty Î±] [Preorder Î±] [IsDirected Î± (Â· â‰¤ Â·)] {f : Î± â†’ Î²} {l : Filter Î²}

theorem tendsto_atTop' : Tendsto f atTop l â†” âˆ€ s âˆˆ l, âˆƒ a, âˆ€ b â‰¥ a, f b âˆˆ s := by
  simp only [tendsto_def, mem_atTop_sets, mem_preimage]

theorem tendsto_atTop_principal {s : Set Î²} : Tendsto f atTop (ğ“Ÿ s) â†” âˆƒ N, âˆ€ n â‰¥ N, f n âˆˆ s := by
  simp_rw [tendsto_iff_comap, comap_principal, le_principal_iff, mem_atTop_sets, mem_preimage]

variable [Preorder Î²]

/-- A function `f` grows to `+âˆ` independent of an order-preserving embedding `e`. -/
theorem tendsto_atTop_atTop : Tendsto f atTop atTop â†” âˆ€ b : Î², âˆƒ i : Î±, âˆ€ a : Î±, i â‰¤ a â†’ b â‰¤ f a :=
  tendsto_iInf.trans <| forall_congr' fun _ => tendsto_atTop_principal

theorem tendsto_atTop_atBot : Tendsto f atTop atBot â†” âˆ€ b : Î², âˆƒ i : Î±, âˆ€ a : Î±, i â‰¤ a â†’ f a â‰¤ b :=
  tendsto_atTop_atTop (Î² := Î²áµ’áµˆ)

theorem tendsto_atTop_atTop_iff_of_monotone (hf : Monotone f) :
    Tendsto f atTop atTop â†” âˆ€ b : Î², âˆƒ a, b â‰¤ f a :=
  tendsto_atTop_atTop.trans <| forall_congr' fun _ => exists_congr fun a =>
    âŸ¨fun h => h a (le_refl a), fun h _a' ha' => le_trans h <| hf ha'âŸ©

theorem tendsto_atTop_atBot_iff_of_antitone (hf : Antitone f) :
    Tendsto f atTop atBot â†” âˆ€ b : Î², âˆƒ a, f a â‰¤ b :=
  tendsto_atTop_atTop_iff_of_monotone (Î² := Î²áµ’áµˆ) hf

end IsDirected

section IsCodirected
variable [Nonempty Î±] [Preorder Î±] [IsDirected Î± (Â· â‰¥ Â·)] {f : Î± â†’ Î²} {l : Filter Î²}

theorem tendsto_atBot' : Tendsto f atBot l â†” âˆ€ s âˆˆ l, âˆƒ a, âˆ€ b â‰¤ a, f b âˆˆ s :=
  tendsto_atTop' (Î± := Î±áµ’áµˆ)

theorem tendsto_atBot_principal {s : Set Î²} : Tendsto f atBot (ğ“Ÿ s) â†” âˆƒ N, âˆ€ n â‰¤ N, f n âˆˆ s :=
  tendsto_atTop_principal (Î± := Î±áµ’áµˆ) (Î² := Î²áµ’áµˆ)

variable [Preorder Î²]

theorem tendsto_atBot_atTop : Tendsto f atBot atTop â†” âˆ€ b : Î², âˆƒ i : Î±, âˆ€ a : Î±, a â‰¤ i â†’ b â‰¤ f a :=
  tendsto_atTop_atTop (Î± := Î±áµ’áµˆ)

theorem tendsto_atBot_atBot : Tendsto f atBot atBot â†” âˆ€ b : Î², âˆƒ i : Î±, âˆ€ a : Î±, a â‰¤ i â†’ f a â‰¤ b :=
  tendsto_atTop_atTop (Î± := Î±áµ’áµˆ) (Î² := Î²áµ’áµˆ)

theorem tendsto_atBot_atBot_iff_of_monotone (hf : Monotone f) :
    Tendsto f atBot atBot â†” âˆ€ b : Î², âˆƒ a, f a â‰¤ b :=
  tendsto_atBot_atBot.trans <| forall_congr' fun _ => exists_congr fun a =>
    âŸ¨fun h => h a (le_refl a), fun h _a' ha' => le_trans (hf ha') hâŸ©

theorem tendsto_atBot_atTop_iff_of_antitone (hf : Antitone f) :
    Tendsto f atBot atTop â†” âˆ€ b : Î², âˆƒ a, b â‰¤ f a :=
  tendsto_atBot_atBot_iff_of_monotone (Î² := Î²áµ’áµˆ) hf

end IsCodirected

alias _root_.Monotone.tendsto_atTop_atTop_iff := tendsto_atTop_atTop_iff_of_monotone

alias _root_.Monotone.tendsto_atBot_atBot_iff := tendsto_atBot_atBot_iff_of_monotone

theorem Tendsto.subseq_mem {F : Filter Î±} {V : â„• â†’ Set Î±} (h : âˆ€ n, V n âˆˆ F) {u : â„• â†’ Î±}
    (hu : Tendsto u atTop F) : âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ âˆ€ n, u (Ï† n) âˆˆ V n :=
  extraction_forall_of_eventually'
    (fun n => tendsto_atTop'.mp hu _ (h n) : âˆ€ n, âˆƒ N, âˆ€ k â‰¥ N, u k âˆˆ V n)

theorem eventually_atBot_prod_self [Nonempty Î±] [Preorder Î±] [IsDirected Î± (Â· â‰¥ Â·)]
    {p : Î± Ã— Î± â†’ Prop} : (âˆ€á¶  x in atBot, p x) â†” âˆƒ a, âˆ€ k l, k â‰¤ a â†’ l â‰¤ a â†’ p (k, l) := by
  simp [â† prod_atBot_atBot_eq, (@atBot_basis Î± _ _).prod_self.eventually_iff]

theorem eventually_atTop_prod_self [Nonempty Î±] [Preorder Î±] [IsDirected Î± (Â· â‰¤ Â·)]
    {p : Î± Ã— Î± â†’ Prop} : (âˆ€á¶  x in atTop, p x) â†” âˆƒ a, âˆ€ k l, a â‰¤ k â†’ a â‰¤ l â†’ p (k, l) :=
  eventually_atBot_prod_self (Î± := Î±áµ’áµˆ)

theorem eventually_atBot_prod_self'  [Nonempty Î±] [Preorder Î±] [IsDirected Î± (Â· â‰¥ Â·)]
    {p : Î± Ã— Î± â†’ Prop} : (âˆ€á¶  x in atBot, p x) â†” âˆƒ a, âˆ€ k â‰¤ a, âˆ€ l â‰¤ a, p (k, l) := by
  simp only [eventually_atBot_prod_self, forall_cond_comm]

theorem eventually_atTop_prod_self' [Nonempty Î±] [Preorder Î±] [IsDirected Î± (Â· â‰¤ Â·)]
    {p : Î± Ã— Î± â†’ Prop} : (âˆ€á¶  x in atTop, p x) â†” âˆƒ a, âˆ€ k â‰¥ a, âˆ€ l â‰¥ a, p (k, l) := by
  simp only [eventually_atTop_prod_self, forall_cond_comm]

theorem eventually_atTop_curry [Preorder Î±] [Preorder Î²] {p : Î± Ã— Î² â†’ Prop}
    (hp : âˆ€á¶  x : Î± Ã— Î² in Filter.atTop, p x) : âˆ€á¶  k in atTop, âˆ€á¶  l in atTop, p (k, l) := by
  rw [â† prod_atTop_atTop_eq] at hp
  exact hp.curry

theorem eventually_atBot_curry [Preorder Î±] [Preorder Î²] {p : Î± Ã— Î² â†’ Prop}
    (hp : âˆ€á¶  x : Î± Ã— Î² in Filter.atBot, p x) : âˆ€á¶  k in atBot, âˆ€á¶  l in atBot, p (k, l) :=
  @eventually_atTop_curry Î±áµ’áµˆ Î²áµ’áµˆ _ _ _ hp

/-- A function `f` maps upwards closed sets (atTop sets) to upwards closed sets when it is a
Galois insertion. The Galois "insertion" and "connection" is weakened to only require it to be an
insertion and a connection above `b`. -/
theorem map_atTop_eq_of_gc_preorder
    [Preorder Î±] [IsDirected Î± (Â· â‰¤ Â·)] [Preorder Î²] [IsDirected Î² (Â· â‰¤ Â·)] {f : Î± â†’ Î²}
    (hf : Monotone f) (b : Î²)
    (hgi : âˆ€ c â‰¥ b, âˆƒ x, f x = c âˆ§ âˆ€ a, f a â‰¤ c â†” a â‰¤ x) : map f atTop = atTop := by
  have : Nonempty Î± := (hgi b le_rfl).nonempty
  choose! g hfg hgle using hgi
  refine le_antisymm (hf.tendsto_atTop_atTop fun c â†¦ ?_) ?_
  Â· rcases exists_ge_ge c b with âŸ¨d, hcd, hbdâŸ©
    exact âŸ¨g d, hcd.trans (hfg d hbd).geâŸ©
  Â· have : Nonempty Î± := âŸ¨g bâŸ©
    rw [(atTop_basis.map f).ge_iff]
    intro a _
    filter_upwards [eventually_ge_atTop (f a), eventually_ge_atTop b] with c hac hbc
    exact âŸ¨g c, (hgle _ hbc _).1 hac, hfg _ hbcâŸ©


/-- A function `f` maps upwards closed sets (atTop sets) to upwards closed sets when it is a
Galois insertion. The Galois "insertion" and "connection" is weakened to only require it to be an
insertion and a connection above `b`. -/
theorem map_atTop_eq_of_gc
    [Preorder Î±] [IsDirected Î± (Â· â‰¤ Â·)] [PartialOrder Î²] [IsDirected Î² (Â· â‰¤ Â·)]
    {f : Î± â†’ Î²} (g : Î² â†’ Î±) (b : Î²) (hf : Monotone f)
    (gc : âˆ€ a, âˆ€ c â‰¥ b, f a â‰¤ c â†” a â‰¤ g c) (hgi : âˆ€ c â‰¥ b, c â‰¤ f (g c)) :
    map f atTop = atTop :=
  map_atTop_eq_of_gc_preorder hf b fun c hc â†¦
    âŸ¨g c, le_antisymm ((gc _ _ hc).2 le_rfl) (hgi c hc), (gc Â· c hc)âŸ©

theorem map_atBot_eq_of_gc_preorder
    [Preorder Î±] [IsDirected Î± (Â· â‰¥ Â·)] [Preorder Î²] [IsDirected Î² (Â· â‰¥ Â·)] {f : Î± â†’ Î²}
    (hf : Monotone f) (b : Î²)
    (hgi : âˆ€ c â‰¤ b, âˆƒ x, f x = c âˆ§ âˆ€ a, c â‰¤ f a â†” x â‰¤ a) : map f atBot = atBot :=
  map_atTop_eq_of_gc_preorder (Î± := Î±áµ’áµˆ) (Î² := Î²áµ’áµˆ) hf.dual _ hgi

theorem map_atBot_eq_of_gc [Preorder Î±] [IsDirected Î± (Â· â‰¥ Â·)]
    [PartialOrder Î²] [IsDirected Î² (Â· â‰¥ Â·)] {f : Î± â†’ Î²} (g : Î² â†’ Î±) (b' : Î²)
    (hf : Monotone f) (gc : âˆ€ a, âˆ€ b â‰¤ b', b â‰¤ f a â†” g b â‰¤ a) (hgi : âˆ€ b â‰¤ b', f (g b) â‰¤ b) :
    map f atBot = atBot :=
  map_atTop_eq_of_gc (Î± := Î±áµ’áµˆ) (Î² := Î²áµ’áµˆ) _ _ hf.dual gc hgi

theorem map_val_atTop_of_Ici_subset [Preorder Î±] [IsDirected Î± (Â· â‰¤ Â·)] {a : Î±} {s : Set Î±}
    (h : Ici a âŠ† s) : map ((â†‘) : s â†’ Î±) atTop = atTop := by
  choose f hl hr using exists_ge_ge (Î± := Î±)
  have : DirectedOn (Â· â‰¤ Â·) s := fun x _ y _ â†¦
    âŸ¨f a (f x y), h <| hl _ _, (hl x y).trans (hr _ _), (hr x y).trans (hr _ _)âŸ©
  have : IsDirected s (Â· â‰¤ Â·) := by
    rw [directedOn_iff_directed] at this
    rwa [â† directed_id_iff]
  refine map_atTop_eq_of_gc_preorder (Subtype.mono_coe _) a fun c hc â†¦ ?_
  exact âŸ¨âŸ¨c, h hcâŸ©, rfl, fun _ â†¦ .rflâŸ©

@[simp]
theorem _root_.Nat.map_cast_int_atTop : map ((â†‘) : â„• â†’ â„¤) atTop = atTop := by
  refine map_atTop_eq_of_gc_preorder (fun _ _ â†¦ Int.ofNat_le.2) 0 fun n hn â†¦ ?_
  lift n to â„• using hn
  exact âŸ¨n, rfl, fun _ â†¦ Int.ofNat_leâŸ©

/-- The image of the filter `atTop` on `Ici a` under the coercion equals `atTop`. -/
@[simp]
theorem map_val_Ici_atTop [Preorder Î±] [IsDirected Î± (Â· â‰¤ Â·)] (a : Î±) :
    map ((â†‘) : Ici a â†’ Î±) atTop = atTop :=
  map_val_atTop_of_Ici_subset Subset.rfl

/-- The image of the filter `atTop` on `Ioi a` under the coercion equals `atTop`. -/
@[simp]
theorem map_val_Ioi_atTop [Preorder Î±] [IsDirected Î± (Â· â‰¤ Â·)] [NoMaxOrder Î±] (a : Î±) :
    map ((â†‘) : Ioi a â†’ Î±) atTop = atTop :=
  let âŸ¨_b, hbâŸ© := exists_gt a
  map_val_atTop_of_Ici_subset <| Ici_subset_Ioi.2 hb

/-- The `atTop` filter for an open interval `Ioi a` comes from the `atTop` filter in the ambient
order. -/
theorem atTop_Ioi_eq [Preorder Î±] [IsDirected Î± (Â· â‰¤ Â·)] (a : Î±) :
    atTop = comap ((â†‘) : Ioi a â†’ Î±) atTop := by
  rcases isEmpty_or_nonempty (Ioi a) with h|âŸ¨âŸ¨b, hbâŸ©âŸ©
  Â· subsingleton
  Â· rw [â† map_val_atTop_of_Ici_subset (Ici_subset_Ioi.2 hb), comap_map Subtype.coe_injective]

/-- The `atTop` filter for an open interval `Ici a` comes from the `atTop` filter in the ambient
order. -/
theorem atTop_Ici_eq [Preorder Î±] [IsDirected Î± (Â· â‰¤ Â·)] (a : Î±) :
    atTop = comap ((â†‘) : Ici a â†’ Î±) atTop := by
  rw [â† map_val_Ici_atTop a, comap_map Subtype.coe_injective]

/-- The `atBot` filter for an open interval `Iio a` comes from the `atBot` filter in the ambient
order. -/
@[simp]
theorem map_val_Iio_atBot [Preorder Î±] [IsDirected Î± (Â· â‰¥ Â·)] [NoMinOrder Î±] (a : Î±) :
    map ((â†‘) : Iio a â†’ Î±) atBot = atBot :=
  map_val_Ioi_atTop (OrderDual.toDual a)

/-- The `atBot` filter for an open interval `Iio a` comes from the `atBot` filter in the ambient
order. -/
theorem atBot_Iio_eq [Preorder Î±] [IsDirected Î± (Â· â‰¥ Â·)] (a : Î±) :
    atBot = comap ((â†‘) : Iio a â†’ Î±) atBot :=
  atTop_Ioi_eq (OrderDual.toDual a)

/-- The `atBot` filter for an open interval `Iic a` comes from the `atBot` filter in the ambient
order. -/
@[simp]
theorem map_val_Iic_atBot [Preorder Î±] [IsDirected Î± (Â· â‰¥ Â·)] (a : Î±) :
    map ((â†‘) : Iic a â†’ Î±) atBot = atBot :=
  map_val_Ici_atTop (OrderDual.toDual a)

/-- The `atBot` filter for an open interval `Iic a` comes from the `atBot` filter in the ambient
order. -/
theorem atBot_Iic_eq [Preorder Î±] [IsDirected Î± (Â· â‰¥ Â·)] (a : Î±) :
    atBot = comap ((â†‘) : Iic a â†’ Î±) atBot :=
  atTop_Ici_eq (OrderDual.toDual a)

theorem tendsto_Ioi_atTop [Preorder Î±] [IsDirected Î± (Â· â‰¤ Â·)]
    {a : Î±} {f : Î² â†’ Ioi a} {l : Filter Î²} :
    Tendsto f l atTop â†” Tendsto (fun x => (f x : Î±)) l atTop := by
  rw [atTop_Ioi_eq, tendsto_comap_iff, Function.comp_def]

theorem tendsto_Iio_atBot [Preorder Î±] [IsDirected Î± (Â· â‰¥ Â·)]
    {a : Î±} {f : Î² â†’ Iio a} {l : Filter Î²} :
    Tendsto f l atBot â†” Tendsto (fun x => (f x : Î±)) l atBot :=
  tendsto_Ioi_atTop (Î± := Î±áµ’áµˆ)

theorem tendsto_Ici_atTop [Preorder Î±] [IsDirected Î± (Â· â‰¤ Â·)]
    {a : Î±} {f : Î² â†’ Ici a} {l : Filter Î²} :
    Tendsto f l atTop â†” Tendsto (fun x => (f x : Î±)) l atTop := by
  rw [atTop_Ici_eq, tendsto_comap_iff, Function.comp_def]

theorem tendsto_Iic_atBot [Preorder Î±] [IsDirected Î± (Â· â‰¥ Â·)]
    {a : Î±} {f : Î² â†’ Iic a} {l : Filter Î²} :
    Tendsto f l atBot â†” Tendsto (fun x => (f x : Î±)) l atBot :=
  tendsto_Ici_atTop (Î± := Î±áµ’áµˆ)

@[simp]
theorem tendsto_comp_val_Ioi_atTop [Preorder Î±] [IsDirected Î± (Â· â‰¤ Â·)] [NoMaxOrder Î±]
    {a : Î±} {f : Î± â†’ Î²} {l : Filter Î²} :
    Tendsto (fun x : Ioi a => f x) atTop l â†” Tendsto f atTop l := by
  rw [â† map_val_Ioi_atTop a, tendsto_map'_iff, Function.comp_def]

@[simp]
theorem tendsto_comp_val_Ici_atTop [Preorder Î±] [IsDirected Î± (Â· â‰¤ Â·)]
    {a : Î±} {f : Î± â†’ Î²} {l : Filter Î²} :
    Tendsto (fun x : Ici a => f x) atTop l â†” Tendsto f atTop l := by
  rw [â† map_val_Ici_atTop a, tendsto_map'_iff, Function.comp_def]

@[simp]
theorem tendsto_comp_val_Iio_atBot [Preorder Î±] [IsDirected Î± (Â· â‰¥ Â·)] [NoMinOrder Î±]
    {a : Î±} {f : Î± â†’ Î²} {l : Filter Î²} :
    Tendsto (fun x : Iio a => f x) atBot l â†” Tendsto f atBot l :=
  tendsto_comp_val_Ioi_atTop (Î± := Î±áµ’áµˆ)

@[simp]
theorem tendsto_comp_val_Iic_atBot [Preorder Î±] [IsDirected Î± (Â· â‰¥ Â·)]
    {a : Î±} {f : Î± â†’ Î²} {l : Filter Î²} :
    Tendsto (fun x : Iic a => f x) atBot l â†” Tendsto f atBot l :=
  tendsto_comp_val_Ici_atTop (Î± := Î±áµ’áµˆ)

theorem map_add_atTop_eq_nat (k : â„•) : map (fun a => a + k) atTop = atTop :=
  map_atTop_eq_of_gc (Â· - k) k (fun _ _ h => Nat.add_le_add_right h k)
    (fun _ _ h => (Nat.le_sub_iff_add_le h).symm) fun a h => by rw [Nat.sub_add_cancel h]

theorem map_sub_atTop_eq_nat (k : â„•) : map (fun a => a - k) atTop = atTop :=
  map_atTop_eq_of_gc (Â· + k) 0 (fun _ _ h => Nat.sub_le_sub_right h _)
    (fun _ _ _ => Nat.sub_le_iff_le_add) fun b _ => by rw [Nat.add_sub_cancel_right]

theorem tendsto_add_atTop_nat (k : â„•) : Tendsto (fun a => a + k) atTop atTop :=
  le_of_eq (map_add_atTop_eq_nat k)

theorem tendsto_sub_atTop_nat (k : â„•) : Tendsto (fun a => a - k) atTop atTop :=
  le_of_eq (map_sub_atTop_eq_nat k)

theorem tendsto_add_atTop_iff_nat {f : â„• â†’ Î±} {l : Filter Î±} (k : â„•) :
    Tendsto (fun n => f (n + k)) atTop l â†” Tendsto f atTop l :=
  show Tendsto (f âˆ˜ fun n => n + k) atTop l â†” Tendsto f atTop l by
    rw [â† tendsto_map'_iff, map_add_atTop_eq_nat]

theorem map_div_atTop_eq_nat (k : â„•) (hk : 0 < k) : map (fun a => a / k) atTop = atTop :=
  map_atTop_eq_of_gc (fun b => k * b + (k - 1)) 1 (fun _ _ h => Nat.div_le_div_right h)
    -- Porting note: there was a parse error in `calc`, use `simp` instead
    (fun a b _ => by rw [Nat.div_le_iff_le_mul_add_pred hk])
    fun b _ => by rw [Nat.mul_add_div hk, Nat.div_eq_of_lt, add_zero]; omega

section IsDirected
variable [Nonempty Î±] [Preorder Î±] [IsDirected Î± (Â· â‰¤ Â·)] [Preorder Î²] {f : Î± â†’ Î²}

theorem unbounded_of_tendsto_atTop [NoMaxOrder Î²] (h : Tendsto f atTop atTop) :
    Â¬BddAbove (range f) := by
  rintro âŸ¨M, hMâŸ©
  obtain âŸ¨a, haâŸ© := mem_atTop_sets.mp (h <| Ioi_mem_atTop M)
  apply lt_irrefl M
  calc
    M < f a := ha a le_rfl
    _ â‰¤ M := hM (Set.mem_range_self a)

theorem unbounded_of_tendsto_atBot [NoMinOrder Î²] (h : Tendsto f atTop atBot) :
    Â¬BddBelow (range f) := unbounded_of_tendsto_atTop (Î² := Î²áµ’áµˆ) h

end IsDirected

section IsCodirected
variable [Nonempty Î±] [Preorder Î±] [IsDirected Î± (Â· â‰¥ Â·)] [Preorder Î²] {f : Î± â†’ Î²}

theorem unbounded_of_tendsto_atTop' [NoMaxOrder Î²] (h : Tendsto f atBot atTop) :
    Â¬BddAbove (range f) := unbounded_of_tendsto_atTop (Î± := Î±áµ’áµˆ) h

theorem unbounded_of_tendsto_atBot' [NoMinOrder Î²] (h : Tendsto f atBot atBot) :
    Â¬BddBelow (range f) := unbounded_of_tendsto_atTop (Î± := Î±áµ’áµˆ) (Î² := Î²áµ’áµˆ) h

end IsCodirected

theorem HasAntitoneBasis.eventually_subset [Preorder Î¹] {l : Filter Î±} {s : Î¹ â†’ Set Î±}
    (hl : l.HasAntitoneBasis s) {t : Set Î±} (ht : t âˆˆ l) : âˆ€á¶  i in atTop, s i âŠ† t :=
  let âŸ¨i, _, hiâŸ© := hl.1.mem_iff.1 ht
  (eventually_ge_atTop i).mono fun _j hj => (hl.antitone hj).trans hi

protected theorem HasAntitoneBasis.tendsto [Preorder Î¹] {l : Filter Î±} {s : Î¹ â†’ Set Î±}
    (hl : l.HasAntitoneBasis s) {Ï† : Î¹ â†’ Î±} (h : âˆ€ i : Î¹, Ï† i âˆˆ s i) : Tendsto Ï† atTop l :=
  fun _t ht => mem_map.2 <| (hl.eventually_subset ht).mono fun i hi => hi (h i)

theorem HasAntitoneBasis.comp_mono [Nonempty Î¹] [Preorder Î¹] [IsDirected Î¹ (Â· â‰¤ Â·)] [Preorder Î¹']
    {l : Filter Î±}
    {s : Î¹' â†’ Set Î±} (hs : l.HasAntitoneBasis s) {Ï† : Î¹ â†’ Î¹'} (Ï†_mono : Monotone Ï†)
    (hÏ† : Tendsto Ï† atTop atTop) : l.HasAntitoneBasis (s âˆ˜ Ï†) :=
  âŸ¨hs.1.to_hasBasis
      (fun n _ => (hÏ†.eventually_ge_atTop n).exists.imp fun _m hm => âŸ¨trivial, hs.antitone hmâŸ©)
      fun n _ => âŸ¨Ï† n, trivial, Subset.rflâŸ©,
    hs.antitone.comp_monotone Ï†_monoâŸ©

theorem HasAntitoneBasis.comp_strictMono {l : Filter Î±} {s : â„• â†’ Set Î±} (hs : l.HasAntitoneBasis s)
    {Ï† : â„• â†’ â„•} (hÏ† : StrictMono Ï†) : l.HasAntitoneBasis (s âˆ˜ Ï†) :=
  hs.comp_mono hÏ†.monotone hÏ†.tendsto_atTop

/-- Given an antitone basis `s : â„• â†’ Set Î±` of a filter, extract an antitone subbasis `s âˆ˜ Ï†`,
`Ï† : â„• â†’ â„•`, such that `m < n` implies `r (Ï† m) (Ï† n)`. This lemma can be used to extract an
antitone basis with basis sets decreasing "sufficiently fast". -/
theorem HasAntitoneBasis.subbasis_with_rel {f : Filter Î±} {s : â„• â†’ Set Î±}
    (hs : f.HasAntitoneBasis s) {r : â„• â†’ â„• â†’ Prop} (hr : âˆ€ m, âˆ€á¶  n in atTop, r m n) :
    âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ (âˆ€ â¦ƒm nâ¦„, m < n â†’ r (Ï† m) (Ï† n)) âˆ§ f.HasAntitoneBasis (s âˆ˜ Ï†) := by
  rsuffices âŸ¨Ï†, hÏ†, hrÏ†âŸ© : âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ âˆ€ m n, m < n â†’ r (Ï† m) (Ï† n)
  Â· exact âŸ¨Ï†, hÏ†, hrÏ†, hs.comp_strictMono hÏ†âŸ©
  have : âˆ€ t : Set â„•, t.Finite â†’ âˆ€á¶  n in atTop, âˆ€ m âˆˆ t, m < n âˆ§ r m n := fun t ht =>
    (eventually_all_finite ht).2 fun m _ => (eventually_gt_atTop m).and (hr _)
  rcases seq_of_forall_finite_exists fun t ht => (this t ht).exists with âŸ¨Ï†, hÏ†âŸ©
  simp only [forall_mem_image, forall_and, mem_Iio] at hÏ†
  exact âŸ¨Ï†, forall_swap.2 hÏ†.1, forall_swap.2 hÏ†.2âŸ©

theorem subseq_forall_of_frequently {Î¹ : Type*} {x : â„• â†’ Î¹} {p : Î¹ â†’ Prop} {l : Filter Î¹}
    (h_tendsto : Tendsto x atTop l) (h : âˆƒá¶  n in atTop, p (x n)) :
    âˆƒ ns : â„• â†’ â„•, Tendsto (fun n => x (ns n)) atTop l âˆ§ âˆ€ n, p (x (ns n)) := by
  choose ns hge hns using frequently_atTop.1 h
  exact âŸ¨ns, h_tendsto.comp (tendsto_atTop_mono hge tendsto_id), hnsâŸ©

end Filter

open Filter Finset

namespace Nat

theorem eventually_pow_lt_factorial_sub (c d : â„•) : âˆ€á¶  n in atTop, c ^ n < (n - d)! := by
  rw [eventually_atTop]
  refine âŸ¨2 * (c ^ 2 + d + 1), ?_âŸ©
  intro n hn
  obtain âŸ¨d', rflâŸ© := Nat.exists_eq_add_of_le hn
  obtain (rfl | c0) := c.eq_zero_or_pos
  Â· simp [Nat.two_mul, â† Nat.add_assoc, Nat.add_right_comm _ 1, Nat.factorial_pos]
  refine (Nat.le_mul_of_pos_right _ (Nat.pow_pos (n := d') c0)).trans_lt ?_
  convert_to (c ^ 2) ^ (c ^ 2 + d' + d + 1) < (c ^ 2 + (c ^ 2 + d' + d + 1) + 1)!
  Â· rw [â† pow_mul, â† pow_add]
    congr 1
    omega
  Â· congr 1
    omega
  refine (lt_of_lt_of_le ?_ Nat.factorial_mul_pow_le_factorial).trans_le <|
    (factorial_le (Nat.le_succ _))
  rw [â† one_mul (_ ^ _ : â„•)]
  apply Nat.mul_lt_mul_of_le_of_lt
  Â· exact Nat.one_le_of_lt (Nat.factorial_pos _)
  Â· exact Nat.pow_lt_pow_left (Nat.lt_succ_self _) (Nat.succ_ne_zero _)
  Â· exact (Nat.factorial_pos _)

theorem eventually_mul_pow_lt_factorial_sub (a c d : â„•) :
    âˆ€á¶  n in atTop, a * c ^ n < (n - d)! := by
  filter_upwards [Nat.eventually_pow_lt_factorial_sub (a * c) d, Filter.eventually_gt_atTop 0]
    with n hn hn0
  rw [mul_pow] at hn
  exact (Nat.mul_le_mul_right _ (Nat.le_self_pow hn0.ne' _)).trans_lt hn

@[deprecated eventually_pow_lt_factorial_sub (since := "2024-09-25")]
theorem exists_pow_lt_factorial (c : â„•) : âˆƒ n0 > 1, âˆ€ n â‰¥ n0, c ^ n < (n - 1)! :=
  let âŸ¨n0, hâŸ© := (eventually_pow_lt_factorial_sub c 1).exists_forall_of_atTop
  âŸ¨max n0 2, by omega, fun n hn â†¦ h n (by omega)âŸ©

@[deprecated eventually_mul_pow_lt_factorial_sub (since := "2024-09-25")]
theorem exists_mul_pow_lt_factorial (a : â„•) (c : â„•) : âˆƒ n0, âˆ€ n â‰¥ n0, a * c ^ n < (n - 1)! :=
  (eventually_mul_pow_lt_factorial_sub a c 1).exists_forall_of_atTop

end Nat
