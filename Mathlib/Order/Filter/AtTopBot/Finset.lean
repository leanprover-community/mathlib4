/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Jeremy Avigad, Yury Kudryashov, Patrick Massot
-/
import Mathlib.Data.Finset.Order
import Mathlib.Order.Filter.AtTopBot.Basic
import Mathlib.Order.Filter.Finite
import Mathlib.Order.Interval.Finset.Defs

/-!
# `Filter.atTop` and `Filter.atBot` filters and finite sets.
-/

variable {Œπ Œπ' Œ± Œ≤ Œ≥ : Type*}

open Set

namespace Filter

theorem tendsto_finset_range : Tendsto Finset.range atTop atTop :=
  Finset.range_mono.tendsto_atTop_atTop Finset.exists_nat_subset_range

theorem atTop_finset_eq_iInf : (atTop : Filter (Finset Œ±)) = ‚®Ö x : Œ±, ùìü (Ici {x}) := by
  refine le_antisymm (le_iInf fun i => le_principal_iff.2 <| mem_atTop ({i} : Finset Œ±)) ?_
  refine
    le_iInf fun s =>
      le_principal_iff.2 <| mem_iInf_of_iInter s.finite_toSet (fun i => mem_principal_self _) ?_
  simp only [subset_def, mem_iInter, SetCoe.forall, mem_Ici, Finset.le_iff_subset,
    Finset.mem_singleton, Finset.subset_iff, forall_eq]
  exact fun t => id

/-- If `f` is a monotone sequence of `Finset`s and each `x` belongs to one of `f n`, then
`Tendsto f atTop atTop`. -/
theorem tendsto_atTop_finset_of_monotone [Preorder Œ≤] {f : Œ≤ ‚Üí Finset Œ±} (h : Monotone f)
    (h' : ‚àÄ x : Œ±, ‚àÉ n, x ‚àà f n) : Tendsto f atTop atTop := by
  simp only [atTop_finset_eq_iInf, tendsto_iInf, tendsto_principal]
  intro a
  rcases h' a with ‚ü®b, hb‚ü©
  exact (eventually_ge_atTop b).mono fun b' hb' => (Finset.singleton_subset_iff.2 hb).trans (h hb')

alias _root_.Monotone.tendsto_atTop_finset := tendsto_atTop_finset_of_monotone

theorem tendsto_finset_image_atTop_atTop [DecidableEq Œ≤] {i : Œ≤ ‚Üí Œ≥} {j : Œ≥ ‚Üí Œ≤}
    (h : Function.LeftInverse j i) : Tendsto (Finset.image j) atTop atTop :=
  (Finset.image_mono j).tendsto_atTop_finset fun a =>
    ‚ü®{i a}, by simp only [Finset.image_singleton, h a, Finset.mem_singleton]‚ü©

theorem tendsto_finset_preimage_atTop_atTop {f : Œ± ‚Üí Œ≤} (hf : Function.Injective f) :
    Tendsto (fun s : Finset Œ≤ => s.preimage f (hf.injOn)) atTop atTop :=
  (Finset.monotone_preimage hf).tendsto_atTop_finset fun x =>
    ‚ü®{f x}, Finset.mem_preimage.2 <| Finset.mem_singleton_self _‚ü©

lemma tendsto_toLeft_atTop :
    Tendsto (Finset.toLeft (Œ± := Œ±) (Œ≤ := Œ≤)) atTop atTop := by
  intro s hs
  simp only [mem_atTop_sets, Filter.mem_map, Set.mem_preimage] at hs ‚ä¢
  obtain ‚ü®t, H‚ü© := hs
  exact ‚ü®t.disjSum ‚àÖ, fun b hb ‚Ü¶ H _ (by simpa [‚Üê Finset.coe_subset, Set.subset_def] using hb)‚ü©

lemma tendsto_toRight_atTop :
    Tendsto (Finset.toRight (Œ± := Œ±) (Œ≤ := Œ≤)) atTop atTop := by
  intro s hs
  simp only [mem_atTop_sets, Filter.mem_map, Set.mem_preimage] at hs ‚ä¢
  obtain ‚ü®t, H‚ü© := hs
  exact ‚ü®.disjSum ‚àÖ t, fun b hb ‚Ü¶ H _ (by simpa [‚Üê Finset.coe_subset, Set.subset_def] using hb)‚ü©

theorem tendsto_finset_powerset_atTop_atTop : Tendsto (Finset.powerset (Œ± := Œ±)) atTop atTop := by
  classical
  refine tendsto_atTop_atTop.mpr fun t ‚Ü¶ ‚ü®t.sup id, fun _ hu _ hv ‚Ü¶ ?_‚ü©
  exact Finset.mem_powerset.mpr <| (Finset.le_sup_of_le hv fun _ h ‚Ü¶ h).trans hu

theorem tendsto_finset_Iic_atTop_atTop [Preorder Œ±] [LocallyFiniteOrderBot Œ±] :
    Tendsto (Finset.Iic (Œ± := Œ±)) atTop atTop := by
  rcases isEmpty_or_nonempty Œ± with _ | _
  ¬∑ exact tendsto_of_isEmpty
  by_cases h : IsDirected Œ± (¬∑ ‚â§ ¬∑)
  ¬∑ refine tendsto_atTop_atTop.mpr fun s ‚Ü¶ ?_
    obtain ‚ü®a, ha‚ü© := Finset.exists_le s
    exact ‚ü®a, fun b hb c hc ‚Ü¶ by simpa using (ha c hc).trans hb‚ü©
  ¬∑ obtain h := Filter.atTop_neBot_iff.not.mpr (fun h' ‚Ü¶ h h'.2)
    simp [not_ne_iff.mp <| Filter.neBot_iff.not.mp h]

theorem tendsto_finset_Ici_atBot_atTop [Preorder Œ±] [LocallyFiniteOrderTop Œ±] :
    Tendsto (Finset.Ici (Œ± := Œ±)) atBot atTop :=
  tendsto_finset_Iic_atTop_atTop (Œ± := Œ±·µí·µà)

end Filter
