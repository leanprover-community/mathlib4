/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Jeremy Avigad, Yury Kudryashov, Patrick Massot
-/
import Mathlib.Order.Filter.AtTopBot.Defs
import Mathlib.Order.Interval.Set.Disjoint

/-!
# Disjointness of `Filter.atTop` and `Filter.atBot`
-/

assert_not_exists Finset

variable {Œπ Œπ' Œ± Œ≤ Œ≥ : Type*}

open Set

namespace Filter

theorem disjoint_atBot_principal_Ioi [Preorder Œ±] (x : Œ±) : Disjoint atBot (ùìü (Ioi x)) :=
  disjoint_of_disjoint_of_mem (Iic_disjoint_Ioi le_rfl) (Iic_mem_atBot x) (mem_principal_self _)

theorem disjoint_atTop_principal_Iio [Preorder Œ±] (x : Œ±) : Disjoint atTop (ùìü (Iio x)) :=
  @disjoint_atBot_principal_Ioi Œ±·µí·µà _ _

theorem disjoint_atTop_principal_Iic [Preorder Œ±] [NoTopOrder Œ±] (x : Œ±) :
    Disjoint atTop (ùìü (Iic x)) :=
  disjoint_of_disjoint_of_mem (Iic_disjoint_Ioi le_rfl).symm (Ioi_mem_atTop x)
    (mem_principal_self _)

theorem disjoint_atBot_principal_Ici [Preorder Œ±] [NoBotOrder Œ±] (x : Œ±) :
    Disjoint atBot (ùìü (Ici x)) :=
  @disjoint_atTop_principal_Iic Œ±·µí·µà _ _ _

theorem disjoint_pure_atTop [Preorder Œ±] [NoTopOrder Œ±] (x : Œ±) : Disjoint (pure x) atTop :=
  Disjoint.symm <| (disjoint_atTop_principal_Iic x).mono_right <| le_principal_iff.2 <|
    mem_pure.2 right_mem_Iic

theorem disjoint_pure_atBot [Preorder Œ±] [NoBotOrder Œ±] (x : Œ±) : Disjoint (pure x) atBot :=
  @disjoint_pure_atTop Œ±·µí·µà _ _ _

theorem disjoint_atBot_atTop [PartialOrder Œ±] [Nontrivial Œ±] :
    Disjoint (atBot : Filter Œ±) atTop := by
  rcases exists_pair_ne Œ± with ‚ü®x, y, hne‚ü©
  by_cases hle : x ‚â§ y
  ¬∑ refine disjoint_of_disjoint_of_mem ?_ (Iic_mem_atBot x) (Ici_mem_atTop y)
    exact Iic_disjoint_Ici.2 (hle.lt_of_ne hne).not_ge
  ¬∑ refine disjoint_of_disjoint_of_mem ?_ (Iic_mem_atBot y) (Ici_mem_atTop x)
    exact Iic_disjoint_Ici.2 hle

theorem disjoint_atTop_atBot [PartialOrder Œ±] [Nontrivial Œ±] : Disjoint (atTop : Filter Œ±) atBot :=
  disjoint_atBot_atTop.symm

end Filter
