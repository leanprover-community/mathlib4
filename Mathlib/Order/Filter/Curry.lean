/-
Copyright (c) 2022 Kevin H. Wilson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin H. Wilson
-/
import Mathlib.Order.Filter.Prod

#align_import order.filter.curry from "leanprover-community/mathlib"@"d6fad0e5bf2d6f48da9175d25c3dc5706b3834ce"

/-!
# Curried Filters

This file provides an operation (`Filter.curry`) on filters which provides the equivalence
`âˆ€á¶  a in l, âˆ€á¶  b in l', p (a, b) â†” âˆ€á¶  c in (l.curry l'), p c` (see `Filter.eventually_curry_iff`).

To understand when this operation might arise, it is helpful to think of `âˆ€á¶ ` as a combination of
the quantifiers `âˆƒ âˆ€`. For instance, `âˆ€á¶  n in at_top, p n â†” âˆƒ N, âˆ€ n â‰¥ N, p n`. A curried filter
yields the quantifier order `âˆƒ âˆ€ âˆƒ âˆ€`. For instance,
`âˆ€á¶  n in at_top.curry at_top, p n â†” âˆƒ M, âˆ€ m â‰¥ M, âˆƒ N, âˆ€ n â‰¥ N, p (m, n)`.

This is different from a product filter, which instead yields a quantifier order `âˆƒ âˆƒ âˆ€ âˆ€`. For
instance, `âˆ€á¶  n in at_top Ã—Ë¢ at_top, p n â†” âˆƒ M, âˆƒ N, âˆ€ m â‰¥ M, âˆ€ n â‰¥ N, p (m, n)`. This makes it
clear that if something eventually occurs on the product filter, it eventually occurs on the curried
filter (see `Filter.curry_le_prod` and `Filter.eventually.curry`), but the converse is not true.

Another way to think about the curried versus the product filter is that tending to some limit on
the product filter is a version of uniform convergence (see `tendsto_prod_filter_iff`) whereas
tending to some limit on a curried filter is just iterated limits (see `Filter.Tendsto.curry`).

## Main definitions

* `Filter.curry`: A binary operation on filters which represents iterated limits

## Main statements

* `Filter.eventually_curry_iff`: An alternative definition of a curried filter
* `Filter.curry_le_prod`: Something that is eventually true on the a product filter is eventually
   true on the curried filter

## Tags

uniform convergence, curried filters, product filters
-/


namespace Filter

variable {Î± Î² Î³ : Type*}

/-- This filter is characterized by `Filter.eventually_curry_iff`:
`(âˆ€á¶  (x : Î± Ã— Î²) in f.curry g, p x) â†” âˆ€á¶  (x : Î±) in f, âˆ€á¶  (y : Î²) in g, p (x, y)`. Useful
in adding quantifiers to the middle of `Tendsto`s. See
`hasFDerivAt_of_tendstoUniformlyOnFilter`. -/
def curry (f : Filter Î±) (g : Filter Î²) : Filter (Î± Ã— Î²) where
  sets := { s | âˆ€á¶  a : Î± in f, âˆ€á¶  b : Î² in g, (a, b) âˆˆ s }
  univ_sets := by simp only [Set.mem_setOf_eq, Set.mem_univ, eventually_true]
                  -- ğŸ‰ no goals
  sets_of_superset := fun hx hxy =>
    hx.mono fun a ha => ha.mono fun b hb => Set.mem_of_subset_of_mem hxy hb
  inter_sets := fun hx hy =>
    (hx.and hy).mono fun a ha => (ha.1.and ha.2).mono fun b hb => hb
#align filter.curry Filter.curry

theorem eventually_curry_iff {f : Filter Î±} {g : Filter Î²} {p : Î± Ã— Î² â†’ Prop} :
    (âˆ€á¶  x : Î± Ã— Î² in f.curry g, p x) â†” âˆ€á¶  x : Î± in f, âˆ€á¶  y : Î² in g, p (x, y) :=
  Iff.rfl
#align filter.eventually_curry_iff Filter.eventually_curry_iff

theorem curry_le_prod {f : Filter Î±} {g : Filter Î²} : f.curry g â‰¤ f.prod g :=
  fun _ => Eventually.curry
#align filter.curry_le_prod Filter.curry_le_prod

theorem Tendsto.curry {f : Î± â†’ Î² â†’ Î³} {la : Filter Î±} {lb : Filter Î²} {lc : Filter Î³}
    (h : âˆ€á¶  a in la, Tendsto (fun b : Î² => f a b) lb lc) : Tendsto (â†¿f) (la.curry lb) lc :=
  fun _s hs => h.mono fun _a ha => ha hs
#align filter.tendsto.curry Filter.Tendsto.curry

end Filter
