/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Johannes HÃ¶lzl, Mario Carneiro, Patrick Massot
-/
import Mathlib.Order.Filter.Bases.Basic
import Mathlib.Order.Filter.Finite

/-!
# Finiteness results on filter bases

A filter basis `B : FilterBasis Î±` on a type `Î±` is a nonempty collection of sets of `Î±`
such that the intersection of two elements of this collection contains some element of
the collection.
-/

open Set Filter

variable {Î± Î² Î³ : Type*} {Î¹ Î¹' : Sort*}

namespace Filter

section SameType

variable {l l' : Filter Î±} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±} {t : Set Î±} {i : Î¹} {p' : Î¹' â†’ Prop}
  {s' : Î¹' â†’ Set Î±} {i' : Î¹'}

theorem hasBasis_generate (s : Set (Set Î±)) :
    (generate s).HasBasis (fun t => Set.Finite t âˆ§ t âŠ† s) fun t => â‹‚â‚€ t :=
  âŸ¨fun U => by simp only [mem_generate_iff, exists_prop, and_assoc, and_left_comm]âŸ©

/-- The smallest filter basis containing a given collection of sets. -/
def FilterBasis.ofSets (s : Set (Set Î±)) : FilterBasis Î± where
  sets := sInter '' { t | Set.Finite t âˆ§ t âŠ† s }
  nonempty := âŸ¨univ, âˆ…, âŸ¨âŸ¨finite_empty, empty_subset sâŸ©, sInter_emptyâŸ©âŸ©
  inter_sets := by
    rintro _ _ âŸ¨a, âŸ¨fina, subaâŸ©, rflâŸ© âŸ¨b, âŸ¨finb, subbâŸ©, rflâŸ©
    exact âŸ¨â‹‚â‚€ (a âˆª b), mem_image_of_mem _ âŸ¨fina.union finb, union_subset suba subbâŸ©,
        (sInter_union _ _).subsetâŸ©

lemma FilterBasis.ofSets_sets (s : Set (Set Î±)) :
    (FilterBasis.ofSets s).sets = sInter '' { t | Set.Finite t âˆ§ t âŠ† s } :=
  rfl

theorem generate_eq_generate_inter (s : Set (Set Î±)) :
    generate s = generate (sInter '' { t | Set.Finite t âˆ§ t âŠ† s }) := by
  rw [â† FilterBasis.ofSets_sets, FilterBasis.generate, â† (hasBasis_generate s).filter_eq]; rfl

theorem ofSets_filter_eq_generate (s : Set (Set Î±)) :
    (FilterBasis.ofSets s).filter = generate s := by
  rw [â† (FilterBasis.ofSets s).generate, FilterBasis.ofSets_sets, â† generate_eq_generate_inter]

theorem generate_neBot_iff {s : Set (Set Î±)} :
    NeBot (generate s) â†” âˆ€ t, t âŠ† s â†’ t.Finite â†’ (â‹‚â‚€ t).Nonempty :=
  (hasBasis_generate s).neBot_iff.trans <| by simp only [â† and_imp, and_comm]

protected theorem HasBasis.iInf' {Î¹ : Type*} {Î¹' : Î¹ â†’ Type*} {l : Î¹ â†’ Filter Î±}
    {p : âˆ€ i, Î¹' i â†’ Prop} {s : âˆ€ i, Î¹' i â†’ Set Î±} (hl : âˆ€ i, (l i).HasBasis (p i) (s i)) :
    (â¨… i, l i).HasBasis (fun If : Set Î¹ Ã— âˆ€ i, Î¹' i => If.1.Finite âˆ§ âˆ€ i âˆˆ If.1, p i (If.2 i))
      fun If : Set Î¹ Ã— âˆ€ i, Î¹' i => â‹‚ i âˆˆ If.1, s i (If.2 i) :=
  âŸ¨by
    intro t
    constructor
    Â· simp only [mem_iInf', (hl _).mem_iff]
      rintro âŸ¨I, hI, V, hV, -, rfl, -âŸ©
      choose u hu using hV
      exact âŸ¨âŸ¨I, uâŸ©, âŸ¨hI, fun i _ => (hu i).1âŸ©, iInterâ‚‚_mono fun i _ => (hu i).2âŸ©
    Â· rintro âŸ¨âŸ¨I, fâŸ©, âŸ¨hIâ‚, hIâ‚‚âŸ©, hsubâŸ©
      refine mem_of_superset ?_ hsub
      exact (biInter_mem hIâ‚).mpr fun i hi => mem_iInf_of_mem i <| (hl i).mem_of_mem <| hIâ‚‚ _ hiâŸ©

@[deprecated (since := "2025-05-05")]
alias hasBasis_iInf' := HasBasis.iInf'

protected theorem HasBasis.iInf {Î¹ : Type*} {Î¹' : Î¹ â†’ Type*} {l : Î¹ â†’ Filter Î±}
    {p : âˆ€ i, Î¹' i â†’ Prop} {s : âˆ€ i, Î¹' i â†’ Set Î±} (hl : âˆ€ i, (l i).HasBasis (p i) (s i)) :
    (â¨… i, l i).HasBasis
      (fun If : Î£ I : Set Î¹, âˆ€ i : I, Î¹' i => If.1.Finite âˆ§ âˆ€ i : If.1, p i (If.2 i)) fun If =>
      â‹‚ i : If.1, s i (If.2 i) := by
  refine âŸ¨fun t => âŸ¨fun ht => ?_, ?_âŸ©âŸ©
  Â· rcases (HasBasis.iInf' hl).mem_iff.mp ht with âŸ¨âŸ¨I, fâŸ©, âŸ¨hI, hfâŸ©, hsubâŸ©
    exact âŸ¨âŸ¨I, fun i => f iâŸ©, âŸ¨hI, Subtype.forall.mpr hfâŸ©, trans (iInter_subtype _ _) hsubâŸ©
  Â· rintro âŸ¨âŸ¨I, fâŸ©, âŸ¨hI, hfâŸ©, hsubâŸ©
    refine mem_of_superset ?_ hsub
    cases hI.nonempty_fintype
    exact iInter_mem.2 fun i => mem_iInf_of_mem â†‘i <| (hl i).mem_of_mem <| hf _

@[deprecated (since := "2025-05-05")]
alias hasBasis_iInf := HasBasis.iInf

open scoped Function in -- required for scoped `on` notation
theorem _root_.Pairwise.exists_mem_filter_basis_of_disjoint {I} [Finite I] {l : I â†’ Filter Î±}
    {Î¹ : I â†’ Sort*} {p : âˆ€ i, Î¹ i â†’ Prop} {s : âˆ€ i, Î¹ i â†’ Set Î±} (hd : Pairwise (Disjoint on l))
    (h : âˆ€ i, (l i).HasBasis (p i) (s i)) :
    âˆƒ ind : âˆ€ i, Î¹ i, (âˆ€ i, p i (ind i)) âˆ§ Pairwise (Disjoint on fun i => s i (ind i)) := by
  rcases hd.exists_mem_filter_of_disjoint with âŸ¨t, htl, hdâŸ©
  choose ind hp ht using fun i => (h i).mem_iff.1 (htl i)
  exact âŸ¨ind, hp, hd.mono fun i j hij => hij.mono (ht _) (ht _)âŸ©

theorem _root_.Set.PairwiseDisjoint.exists_mem_filter_basis {I : Type*} {l : I â†’ Filter Î±}
    {Î¹ : I â†’ Sort*} {p : âˆ€ i, Î¹ i â†’ Prop} {s : âˆ€ i, Î¹ i â†’ Set Î±} {S : Set I}
    (hd : S.PairwiseDisjoint l) (hS : S.Finite) (h : âˆ€ i, (l i).HasBasis (p i) (s i)) :
    âˆƒ ind : âˆ€ i, Î¹ i, (âˆ€ i, p i (ind i)) âˆ§ S.PairwiseDisjoint fun i => s i (ind i) := by
  rcases hd.exists_mem_filter hS with âŸ¨t, htl, hdâŸ©
  choose ind hp ht using fun i => (h i).mem_iff.1 (htl i)
  exact âŸ¨ind, hp, hd.mono htâŸ©

/-- If `s : Î¹ â†’ Set Î±` is an indexed family of sets, then finite intersections of `s i` form a basis
of `â¨… i, ğ“Ÿ (s i)`. -/
theorem hasBasis_iInf_principal_finite {Î¹ : Type*} (s : Î¹ â†’ Set Î±) :
    (â¨… i, ğ“Ÿ (s i)).HasBasis (fun t : Set Î¹ => t.Finite) fun t => â‹‚ i âˆˆ t, s i := by
  refine âŸ¨fun U => (mem_iInf_finite _).trans ?_âŸ©
  simp only [iInf_principal_finset, mem_iUnion, mem_principal, exists_prop,
    exists_finite_iff_finset, Finset.set_biInter_coe]

end SameType

end Filter
