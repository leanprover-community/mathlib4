/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Johannes HÃ¶lzl, Mario Carneiro, Patrick Massot
-/
import Mathlib.Order.Filter.Defs
import Mathlib.Order.Interval.Set.Basic

/-!
# Filter bases

A filter basis `B : FilterBasis Î±` on a type `Î±` is a nonempty collection of sets of `Î±`
such that the intersection of two elements of this collection contains some element of
the collection. Compared to filters, filter bases do not require that any set containing
an element of `B` belongs to `B`.
A filter basis `B` can be used to construct `B.filter : Filter Î±` such that a set belongs
to `B.filter` if and only if it contains an element of `B`.

Given an indexing type `Î¹`, a predicate `p : Î¹ â†’ Prop`, and a map `s : Î¹ â†’ Set Î±`,
the proposition `h : Filter.IsBasis p s` makes sure the range of `s` bounded by `p`
(ie. `s '' setOf p`) defines a filter basis `h.filterBasis`.

If one already has a filter `l` on `Î±`, `Filter.HasBasis l p s` (where `p : Î¹ â†’ Prop`
and `s : Î¹ â†’ Set Î±` as above) means that a set belongs to `l` if and
only if it contains some `s i` with `p i`. It implies `h : Filter.IsBasis p s`, and
`l = h.filterBasis.filter`. The point of this definition is that checking statements
involving elements of `l` often reduces to checking them on the basis elements.

We define a function `HasBasis.index (h : Filter.HasBasis l p s) (t) (ht : t âˆˆ l)` that returns
some index `i` such that `p i` and `s i âŠ† t`. This function can be useful to avoid manual
destruction of `h.mem_iff.mpr ht` using `cases` or `let`.

## Main statements

* `Filter.HasBasis.mem_iff`, `HasBasis.mem_of_superset`, `HasBasis.mem_of_mem` : restate `t âˆˆ f` in
  terms of a basis;

* `Filter.HasBasis.le_iff`, `Filter.HasBasis.ge_iff`, `Filter.HasBasis.le_basis_iff` : restate
  `l â‰¤ l'` in terms of bases.

## Implementation notes

As with `Set.iUnion`/`biUnion`/`Set.sUnion`, there are three different approaches to filter bases:

* `Filter.HasBasis l s`, `s : Set (Set Î±)`;
* `Filter.HasBasis l s`, `s : Î¹ â†’ Set Î±`;
* `Filter.HasBasis l p s`, `p : Î¹ â†’ Prop`, `s : Î¹ â†’ Set Î±`.

We use the latter one because, e.g., `ð“ x` in an `EMetricSpace` or in a `MetricSpace` has a basis
of this form. The other two can be emulated using `s = id` or `p = fun _ â†¦ True`.

With this approach sometimes one needs to `simp` the statement provided by the `Filter.HasBasis`
machinery, e.g., `simp only [true_and_iff]` or `simp only [forall_const]` can help with the case
`p = fun _ â†¦ True`.
-/

assert_not_exists Finset

open Set Filter

variable {Î± Î² Î³ : Type*} {Î¹ Î¹' : Sort*}

/-- A filter basis `B` on a type `Î±` is a nonempty collection of sets of `Î±`
such that the intersection of two elements of this collection contains some element
of the collection. -/
structure FilterBasis (Î± : Type*) where
  /-- Sets of a filter basis. -/
  sets : Set (Set Î±)
  /-- The set of filter basis sets is nonempty. -/
  nonempty : sets.Nonempty
  /-- The set of filter basis sets is directed downwards. -/
  inter_sets {x y} : x âˆˆ sets â†’ y âˆˆ sets â†’ âˆƒ z âˆˆ sets, z âŠ† x âˆ© y

instance FilterBasis.nonempty_sets (B : FilterBasis Î±) : Nonempty B.sets :=
  B.nonempty.to_subtype

-- Porting note: this instance was reducible but it doesn't work the same way in Lean 4
/-- If `B` is a filter basis on `Î±`, and `U` a subset of `Î±` then we can write `U âˆˆ B` as
on paper. -/
instance {Î± : Type*} : Membership (Set Î±) (FilterBasis Î±) :=
  âŸ¨fun B U => U âˆˆ B.setsâŸ©

@[simp] theorem FilterBasis.mem_sets {s : Set Î±} {B : FilterBasis Î±} : s âˆˆ B.sets â†” s âˆˆ B := Iff.rfl

-- For illustration purposes, the filter basis defining `(atTop : Filter â„•)`
instance : Inhabited (FilterBasis â„•) :=
  âŸ¨{  sets := range Ici
      nonempty := âŸ¨Ici 0, mem_range_self 0âŸ©
      inter_sets := by
        rintro _ _ âŸ¨n, rflâŸ© âŸ¨m, rflâŸ©
        exact âŸ¨Ici (max n m), mem_range_self _, Ici_inter_Ici.symm.subsetâŸ© }âŸ©

/-- View a filter as a filter basis. -/
def Filter.asBasis (f : Filter Î±) : FilterBasis Î± :=
  âŸ¨f.sets, âŸ¨univ, univ_memâŸ©, fun {x y} hx hy => âŸ¨x âˆ© y, inter_mem hx hy, subset_rflâŸ©âŸ©

-- Porting note: was `protected` in Lean 3 but `protected` didn't work; removed
/-- `IsBasis p s` means the image of `s` bounded by `p` is a filter basis. -/
structure Filter.IsBasis (p : Î¹ â†’ Prop) (s : Î¹ â†’ Set Î±) : Prop where
  /-- There exists at least one `i` that satisfies `p`. -/
  nonempty : âˆƒ i, p i
  /-- `s` is directed downwards on `i` such that `p i`. -/
  inter : âˆ€ {i j}, p i â†’ p j â†’ âˆƒ k, p k âˆ§ s k âŠ† s i âˆ© s j

namespace Filter

namespace IsBasis

/-- Constructs a filter basis from an indexed family of sets satisfying `IsBasis`. -/
protected def filterBasis {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±} (h : IsBasis p s) : FilterBasis Î± where
  sets := { t | âˆƒ i, p i âˆ§ s i = t }
  nonempty :=
    let âŸ¨i, hiâŸ© := h.nonempty
    âŸ¨s i, âŸ¨i, hi, rflâŸ©âŸ©
  inter_sets := by
    rintro _ _ âŸ¨i, hi, rflâŸ© âŸ¨j, hj, rflâŸ©
    rcases h.inter hi hj with âŸ¨k, hk, hk'âŸ©
    exact âŸ¨_, âŸ¨k, hk, rflâŸ©, hk'âŸ©

variable {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±} (h : IsBasis p s)

theorem mem_filterBasis_iff {U : Set Î±} : U âˆˆ h.filterBasis â†” âˆƒ i, p i âˆ§ s i = U :=
  Iff.rfl

end IsBasis

end Filter

namespace FilterBasis

/-- The filter associated to a filter basis. -/
protected def filter (B : FilterBasis Î±) : Filter Î± where
  sets := { s | âˆƒ t âˆˆ B, t âŠ† s }
  univ_sets := B.nonempty.imp fun s s_in => âŸ¨s_in, s.subset_univâŸ©
  sets_of_superset := fun âŸ¨s, s_in, hâŸ© hxy => âŸ¨s, s_in, Set.Subset.trans h hxyâŸ©
  inter_sets := fun âŸ¨_s, s_in, hsâŸ© âŸ¨_t, t_in, htâŸ© =>
    let âŸ¨u, u_in, u_subâŸ© := B.inter_sets s_in t_in
    âŸ¨u, u_in, u_sub.trans (inter_subset_inter hs ht)âŸ©

theorem mem_filter_iff (B : FilterBasis Î±) {U : Set Î±} : U âˆˆ B.filter â†” âˆƒ s âˆˆ B, s âŠ† U :=
  Iff.rfl

theorem mem_filter_of_mem (B : FilterBasis Î±) {U : Set Î±} : U âˆˆ B â†’ U âˆˆ B.filter := fun U_in =>
  âŸ¨U, U_in, Subset.refl _âŸ©

end FilterBasis

namespace Filter

namespace IsBasis

variable {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±}

/-- Constructs a filter from an indexed family of sets satisfying `IsBasis`. -/
protected def filter (h : IsBasis p s) : Filter Î± :=
  h.filterBasis.filter

protected theorem mem_filter_iff (h : IsBasis p s) {U : Set Î±} :
    U âˆˆ h.filter â†” âˆƒ i, p i âˆ§ s i âŠ† U := by
  simp only [IsBasis.filter, FilterBasis.mem_filter_iff, mem_filterBasis_iff,
    exists_exists_and_eq_and]

end IsBasis

-- Porting note: was `protected` in Lean 3 but `protected` didn't work; removed
/-- We say that a filter `l` has a basis `s : Î¹ â†’ Set Î±` bounded by `p : Î¹ â†’ Prop`,
if `t âˆˆ l` if and only if `t` includes `s i` for some `i` such that `p i`. -/
structure HasBasis (l : Filter Î±) (p : Î¹ â†’ Prop) (s : Î¹ â†’ Set Î±) : Prop where
  /-- A set `t` belongs to a filter `l` iff it includes an element of the basis. -/
  mem_iff' : âˆ€ t : Set Î±, t âˆˆ l â†” âˆƒ i, p i âˆ§ s i âŠ† t

section SameType

variable {l l' : Filter Î±} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±} {t : Set Î±} {i : Î¹} {p' : Î¹' â†’ Prop}
  {s' : Î¹' â†’ Set Î±} {i' : Î¹'}

-- Porting note: use `âˆƒ i, p i âˆ§ _` instead of `âˆƒ i (hi : p i), _`.
/-- Definition of `HasBasis` unfolded with implicit set argument. -/
theorem HasBasis.mem_iff (hl : l.HasBasis p s) : t âˆˆ l â†” âˆƒ i, p i âˆ§ s i âŠ† t :=
  hl.mem_iff' t

theorem HasBasis.eq_of_same_basis (hl : l.HasBasis p s) (hl' : l'.HasBasis p s) : l = l' := by
  ext t
  rw [hl.mem_iff, hl'.mem_iff]

-- Porting note: use `âˆƒ i, p i âˆ§ _` instead of `âˆƒ i (hi : p i), _`.
theorem hasBasis_iff : l.HasBasis p s â†” âˆ€ t, t âˆˆ l â†” âˆƒ i, p i âˆ§ s i âŠ† t :=
  âŸ¨fun âŸ¨hâŸ© => h, fun h => âŸ¨hâŸ©âŸ©

theorem HasBasis.ex_mem (h : l.HasBasis p s) : âˆƒ i, p i :=
  (h.mem_iff.mp univ_mem).imp fun _ => And.left

protected theorem HasBasis.nonempty (h : l.HasBasis p s) : Nonempty Î¹ :=
  nonempty_of_exists h.ex_mem

protected theorem IsBasis.hasBasis (h : IsBasis p s) : HasBasis h.filter p s :=
  âŸ¨fun t => by simp only [h.mem_filter_iff, exists_prop]âŸ©

protected theorem HasBasis.mem_of_superset (hl : l.HasBasis p s) (hi : p i) (ht : s i âŠ† t) :
    t âˆˆ l :=
  hl.mem_iff.2 âŸ¨i, hi, htâŸ©

theorem HasBasis.mem_of_mem (hl : l.HasBasis p s) (hi : p i) : s i âˆˆ l :=
  hl.mem_of_superset hi Subset.rfl

/-- Index of a basis set such that `s i âŠ† t` as an element of `Subtype p`. -/
noncomputable def HasBasis.index (h : l.HasBasis p s) (t : Set Î±) (ht : t âˆˆ l) : { i : Î¹ // p i } :=
  âŸ¨(h.mem_iff.1 ht).choose, (h.mem_iff.1 ht).choose_spec.1âŸ©

theorem HasBasis.property_index (h : l.HasBasis p s) (ht : t âˆˆ l) : p (h.index t ht) :=
  (h.index t ht).2

theorem HasBasis.set_index_mem (h : l.HasBasis p s) (ht : t âˆˆ l) : s (h.index t ht) âˆˆ l :=
  h.mem_of_mem <| h.property_index _

theorem HasBasis.set_index_subset (h : l.HasBasis p s) (ht : t âˆˆ l) : s (h.index t ht) âŠ† t :=
  (h.mem_iff.1 ht).choose_spec.2

theorem HasBasis.isBasis (h : l.HasBasis p s) : IsBasis p s where
  nonempty := h.ex_mem
  inter hi hj := by
    simpa only [h.mem_iff] using inter_mem (h.mem_of_mem hi) (h.mem_of_mem hj)

theorem HasBasis.filter_eq (h : l.HasBasis p s) : h.isBasis.filter = l := by
  ext U
  simp [h.mem_iff, IsBasis.mem_filter_iff]

protected theorem _root_.FilterBasis.hasBasis (B : FilterBasis Î±) :
    HasBasis B.filter (fun s : Set Î± => s âˆˆ B) id :=
  âŸ¨fun _ => B.mem_filter_iffâŸ©

theorem HasBasis.to_hasBasis' (hl : l.HasBasis p s) (h : âˆ€ i, p i â†’ âˆƒ i', p' i' âˆ§ s' i' âŠ† s i)
    (h' : âˆ€ i', p' i' â†’ s' i' âˆˆ l) : l.HasBasis p' s' := by
  refine âŸ¨fun t => âŸ¨fun ht => ?_, fun âŸ¨i', hi', htâŸ© => mem_of_superset (h' i' hi') htâŸ©âŸ©
  rcases hl.mem_iff.1 ht with âŸ¨i, hi, htâŸ©
  rcases h i hi with âŸ¨i', hi', hs'sâŸ©
  exact âŸ¨i', hi', hs's.trans htâŸ©

theorem HasBasis.to_hasBasis (hl : l.HasBasis p s) (h : âˆ€ i, p i â†’ âˆƒ i', p' i' âˆ§ s' i' âŠ† s i)
    (h' : âˆ€ i', p' i' â†’ âˆƒ i, p i âˆ§ s i âŠ† s' i') : l.HasBasis p' s' :=
  hl.to_hasBasis' h fun i' hi' =>
    let âŸ¨i, hi, hss'âŸ© := h' i' hi'
    hl.mem_iff.2 âŸ¨i, hi, hss'âŸ©

protected lemma HasBasis.congr (hl : l.HasBasis p s) {p' s'} (hp : âˆ€ i, p i â†” p' i)
    (hs : âˆ€ i, p i â†’ s i = s' i) : l.HasBasis p' s' :=
  âŸ¨fun t â†¦ by simp only [hl.mem_iff, â† hp]; exact exists_congr fun i â†¦
    and_congr_right fun hi â†¦ hs i hi â–¸ Iff.rflâŸ©

theorem HasBasis.to_subset (hl : l.HasBasis p s) {t : Î¹ â†’ Set Î±} (h : âˆ€ i, p i â†’ t i âŠ† s i)
    (ht : âˆ€ i, p i â†’ t i âˆˆ l) : l.HasBasis p t :=
  hl.to_hasBasis' (fun i hi => âŸ¨i, hi, h i hiâŸ©) ht

theorem HasBasis.eventually_iff (hl : l.HasBasis p s) {q : Î± â†’ Prop} :
    (âˆ€á¶  x in l, q x) â†” âˆƒ i, p i âˆ§ âˆ€ â¦ƒxâ¦„, x âˆˆ s i â†’ q x := by simpa using hl.mem_iff

theorem HasBasis.frequently_iff (hl : l.HasBasis p s) {q : Î± â†’ Prop} :
    (âˆƒá¶  x in l, q x) â†” âˆ€ i, p i â†’ âˆƒ x âˆˆ s i, q x := by
  simp only [Filter.Frequently, hl.eventually_iff]; push_neg; rfl

-- Porting note: use `âˆƒ i, p i âˆ§ _` instead of `âˆƒ i (hi : p i), _`.
theorem HasBasis.exists_iff (hl : l.HasBasis p s) {P : Set Î± â†’ Prop}
    (mono : âˆ€ â¦ƒs tâ¦„, s âŠ† t â†’ P t â†’ P s) : (âˆƒ s âˆˆ l, P s) â†” âˆƒ i, p i âˆ§ P (s i) :=
  âŸ¨fun âŸ¨_s, hs, hPâŸ© =>
    let âŸ¨i, hi, hisâŸ© := hl.mem_iff.1 hs
    âŸ¨i, hi, mono his hPâŸ©,
    fun âŸ¨i, hi, hPâŸ© => âŸ¨s i, hl.mem_of_mem hi, hPâŸ©âŸ©

theorem HasBasis.forall_iff (hl : l.HasBasis p s) {P : Set Î± â†’ Prop}
    (mono : âˆ€ â¦ƒs tâ¦„, s âŠ† t â†’ P s â†’ P t) : (âˆ€ s âˆˆ l, P s) â†” âˆ€ i, p i â†’ P (s i) :=
  âŸ¨fun H i hi => H (s i) <| hl.mem_of_mem hi, fun H _s hs =>
    let âŸ¨i, hi, hisâŸ© := hl.mem_iff.1 hs
    mono his (H i hi)âŸ©

theorem hasBasis_self {l : Filter Î±} {P : Set Î± â†’ Prop} :
    HasBasis l (fun s => s âˆˆ l âˆ§ P s) id â†” âˆ€ t âˆˆ l, âˆƒ r âˆˆ l, P r âˆ§ r âŠ† t := by
  simp only [hasBasis_iff, id, and_assoc]
  exact forall_congr' fun s =>
    âŸ¨fun h => h.1, fun h => âŸ¨h, fun âŸ¨t, hl, _, htsâŸ© => mem_of_superset hl htsâŸ©âŸ©

theorem HasBasis.comp_surjective (h : l.HasBasis p s) {g : Î¹' â†’ Î¹} (hg : Function.Surjective g) :
    l.HasBasis (p âˆ˜ g) (s âˆ˜ g) :=
  âŸ¨fun _ => h.mem_iff.trans hg.existsâŸ©

theorem HasBasis.comp_equiv (h : l.HasBasis p s) (e : Î¹' â‰ƒ Î¹) : l.HasBasis (p âˆ˜ e) (s âˆ˜ e) :=
  h.comp_surjective e.surjective

theorem HasBasis.to_image_id' (h : l.HasBasis p s) : l.HasBasis (fun t â†¦ âˆƒ i, p i âˆ§ s i = t) id :=
  âŸ¨fun _ â†¦ by simp [h.mem_iff]âŸ©

theorem HasBasis.to_image_id {Î¹ : Type*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±} (h : l.HasBasis p s) :
    l.HasBasis (Â· âˆˆ s '' {i | p i}) id :=
  h.to_image_id'

/-- If `{s i | p i}` is a basis of a filter `l` and each `s i` includes `s j` such that
`p j âˆ§ q j`, then `{s j | p j âˆ§ q j}` is a basis of `l`. -/
theorem HasBasis.restrict (h : l.HasBasis p s) {q : Î¹ â†’ Prop}
    (hq : âˆ€ i, p i â†’ âˆƒ j, p j âˆ§ q j âˆ§ s j âŠ† s i) : l.HasBasis (fun i => p i âˆ§ q i) s := by
  refine âŸ¨fun t => âŸ¨fun ht => ?_, fun âŸ¨i, hpi, htiâŸ© => h.mem_iff.2 âŸ¨i, hpi.1, htiâŸ©âŸ©âŸ©
  rcases h.mem_iff.1 ht with âŸ¨i, hpi, htiâŸ©
  rcases hq i hpi with âŸ¨j, hpj, hqj, hjiâŸ©
  exact âŸ¨j, âŸ¨hpj, hqjâŸ©, hji.trans htiâŸ©

/-- If `{s i | p i}` is a basis of a filter `l` and `V âˆˆ l`, then `{s i | p i âˆ§ s i âŠ† V}`
is a basis of `l`. -/
theorem HasBasis.restrict_subset (h : l.HasBasis p s) {V : Set Î±} (hV : V âˆˆ l) :
    l.HasBasis (fun i => p i âˆ§ s i âŠ† V) s :=
  h.restrict fun _i hi => (h.mem_iff.1 (inter_mem hV (h.mem_of_mem hi))).imp fun _j hj =>
    âŸ¨hj.1, subset_inter_iff.1 hj.2âŸ©

theorem HasBasis.hasBasis_self_subset {p : Set Î± â†’ Prop} (h : l.HasBasis (fun s => s âˆˆ l âˆ§ p s) id)
    {V : Set Î±} (hV : V âˆˆ l) : l.HasBasis (fun s => s âˆˆ l âˆ§ p s âˆ§ s âŠ† V) id := by
  simpa only [and_assoc] using h.restrict_subset hV

theorem HasBasis.ge_iff (hl' : l'.HasBasis p' s') : l â‰¤ l' â†” âˆ€ i', p' i' â†’ s' i' âˆˆ l :=
  âŸ¨fun h _i' hi' => h <| hl'.mem_of_mem hi', fun h _s hs =>
    let âŸ¨_i', hi', hsâŸ© := hl'.mem_iff.1 hs
    mem_of_superset (h _ hi') hsâŸ©

-- Porting note: use `âˆƒ i, p i âˆ§ _` instead of `âˆƒ i (hi : p i), _`.
theorem HasBasis.le_iff (hl : l.HasBasis p s) : l â‰¤ l' â†” âˆ€ t âˆˆ l', âˆƒ i, p i âˆ§ s i âŠ† t := by
  simp only [le_def, hl.mem_iff]

-- Porting note: use `âˆƒ i, p i âˆ§ _` instead of `âˆƒ i (hi : p i), _`.
theorem HasBasis.le_basis_iff (hl : l.HasBasis p s) (hl' : l'.HasBasis p' s') :
    l â‰¤ l' â†” âˆ€ i', p' i' â†’ âˆƒ i, p i âˆ§ s i âŠ† s' i' := by
  simp only [hl'.ge_iff, hl.mem_iff]

-- Porting note: use `âˆƒ i, p i âˆ§ _` instead of `âˆƒ i (hi : p i), _`.
theorem HasBasis.ext (hl : l.HasBasis p s) (hl' : l'.HasBasis p' s')
    (h : âˆ€ i, p i â†’ âˆƒ i', p' i' âˆ§ s' i' âŠ† s i) (h' : âˆ€ i', p' i' â†’ âˆƒ i, p i âˆ§ s i âŠ† s' i') :
    l = l' := by
  apply le_antisymm
  Â· rw [hl.le_basis_iff hl']
    simpa using h'
  Â· rw [hl'.le_basis_iff hl]
    simpa using h

theorem hasBasis_principal (t : Set Î±) : (ð“Ÿ t).HasBasis (fun _ : Unit => True) fun _ => t :=
  âŸ¨fun U => by simpâŸ©

theorem HasBasis.forall_mem_mem (h : HasBasis l p s) {x : Î±} :
    (âˆ€ t âˆˆ l, x âˆˆ t) â†” âˆ€ i, p i â†’ x âˆˆ s i := by
  simp only [h.mem_iff, exists_imp, and_imp]
  exact âŸ¨fun h i hi => h (s i) i hi Subset.rfl, fun h t i hi ht => ht (h i hi)âŸ©

variable {Î¹'' : Type*} [Preorder Î¹''] (l) (s'' : Î¹'' â†’ Set Î±)

/-- `IsAntitoneBasis s` means the image of `s` is a filter basis such that `s` is decreasing. -/
structure IsAntitoneBasis : Prop extends IsBasis (fun _ => True) s'' where
  /-- The sequence of sets is antitone. -/
  protected antitone : Antitone s''

/-- We say that a filter `l` has an antitone basis `s : Î¹ â†’ Set Î±`, if `t âˆˆ l` if and only if `t`
includes `s i` for some `i`, and `s` is decreasing. -/
structure HasAntitoneBasis (l : Filter Î±) (s : Î¹'' â†’ Set Î±) : Prop
    extends HasBasis l (fun _ => True) s where
  /-- The sequence of sets is antitone. -/
  protected antitone : Antitone s

end SameType

end Filter
