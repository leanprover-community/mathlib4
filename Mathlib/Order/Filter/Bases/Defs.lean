/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Johannes H√∂lzl, Mario Carneiro, Patrick Massot
-/
import Mathlib.Order.Filter.Defs
import Mathlib.Order.Interval.Set.Basic

/-!
# Filter bases

A filter basis `B : FilterBasis Œ±` on a type `Œ±` is a nonempty collection of sets of `Œ±`
such that the intersection of two elements of this collection contains some element of
the collection. Compared to filters, filter bases do not require that any set containing
an element of `B` belongs to `B`.
A filter basis `B` can be used to construct `B.filter : Filter Œ±` such that a set belongs
to `B.filter` if and only if it contains an element of `B`.

Given an indexing type `Œπ`, a predicate `p : Œπ ‚Üí Prop`, and a map `s : Œπ ‚Üí Set Œ±`,
the proposition `h : Filter.IsBasis p s` makes sure the range of `s` bounded by `p`
(ie. `s '' setOf p`) defines a filter basis `h.filterBasis`.

If one already has a filter `l` on `Œ±`, `Filter.HasBasis l p s` (where `p : Œπ ‚Üí Prop`
and `s : Œπ ‚Üí Set Œ±` as above) means that a set belongs to `l` if and
only if it contains some `s i` with `p i`. It implies `h : Filter.IsBasis p s`, and
`l = h.filterBasis.filter`. The point of this definition is that checking statements
involving elements of `l` often reduces to checking them on the basis elements.

We define a function `HasBasis.index (h : Filter.HasBasis l p s) (t) (ht : t ‚àà l)` that returns
some index `i` such that `p i` and `s i ‚äÜ t`. This function can be useful to avoid manual
destruction of `h.mem_iff.mpr ht` using `cases` or `let`.

## Main statements

* `Filter.HasBasis.mem_iff`, `HasBasis.mem_of_superset`, `HasBasis.mem_of_mem` : restate `t ‚àà f` in
  terms of a basis;

* `Filter.HasBasis.le_iff`, `Filter.HasBasis.ge_iff`, `Filter.HasBasis.le_basis_iff` : restate
  `l ‚â§ l'` in terms of bases.

## Implementation notes

As with `Set.iUnion`/`biUnion`/`Set.sUnion`, there are three different approaches to filter bases:

* `Filter.HasBasis l s`, `s : Set (Set Œ±)`;
* `Filter.HasBasis l s`, `s : Œπ ‚Üí Set Œ±`;
* `Filter.HasBasis l p s`, `p : Œπ ‚Üí Prop`, `s : Œπ ‚Üí Set Œ±`.

We use the latter one because, e.g., `ùìù x` in an `EMetricSpace` or in a `MetricSpace` has a basis
of this form. The other two can be emulated using `s = id` or `p = fun _ ‚Ü¶ True`.

With this approach sometimes one needs to `simp` the statement provided by the `Filter.HasBasis`
machinery, e.g., `simp only [true_and_iff]` or `simp only [forall_const]` can help with the case
`p = fun _ ‚Ü¶ True`.
-/

assert_not_exists Finset

open Set Filter

variable {Œ± Œ≤ Œ≥ : Type*} {Œπ Œπ' : Sort*}

/-- A filter basis `B` on a type `Œ±` is a nonempty collection of sets of `Œ±`
such that the intersection of two elements of this collection contains some element
of the collection. -/
structure FilterBasis (Œ± : Type*) where
  /-- Sets of a filter basis. -/
  sets : Set (Set Œ±)
  /-- The set of filter basis sets is nonempty. -/
  nonempty : sets.Nonempty
  /-- The set of filter basis sets is directed downwards. -/
  inter_sets {x y} : x ‚àà sets ‚Üí y ‚àà sets ‚Üí ‚àÉ z ‚àà sets, z ‚äÜ x ‚à© y

instance FilterBasis.nonempty_sets (B : FilterBasis Œ±) : Nonempty B.sets :=
  B.nonempty.to_subtype

-- Porting note: this instance was reducible but it doesn't work the same way in Lean 4
/-- If `B` is a filter basis on `Œ±`, and `U` a subset of `Œ±` then we can write `U ‚àà B` as
on paper. -/
instance {Œ± : Type*} : Membership (Set Œ±) (FilterBasis Œ±) :=
  ‚ü®fun B U => U ‚àà B.sets‚ü©

@[simp] theorem FilterBasis.mem_sets {s : Set Œ±} {B : FilterBasis Œ±} : s ‚àà B.sets ‚Üî s ‚àà B := Iff.rfl

-- For illustration purposes, the filter basis defining `(atTop : Filter ‚Ñï)`
instance : Inhabited (FilterBasis ‚Ñï) :=
  ‚ü®{  sets := range Ici
      nonempty := ‚ü®Ici 0, mem_range_self 0‚ü©
      inter_sets := by
        rintro _ _ ‚ü®n, rfl‚ü© ‚ü®m, rfl‚ü©
        exact ‚ü®Ici (max n m), mem_range_self _, Ici_inter_Ici.symm.subset‚ü© }‚ü©

/-- View a filter as a filter basis. -/
def Filter.asBasis (f : Filter Œ±) : FilterBasis Œ± :=
  ‚ü®f.sets, ‚ü®univ, univ_mem‚ü©, fun {x y} hx hy => ‚ü®x ‚à© y, inter_mem hx hy, subset_rfl‚ü©‚ü©

-- Porting note: was `protected` in Lean 3 but `protected` didn't work; removed
/-- `IsBasis p s` means the image of `s` bounded by `p` is a filter basis. -/
structure Filter.IsBasis (p : Œπ ‚Üí Prop) (s : Œπ ‚Üí Set Œ±) : Prop where
  /-- There exists at least one `i` that satisfies `p`. -/
  nonempty : ‚àÉ i, p i
  /-- `s` is directed downwards on `i` such that `p i`. -/
  inter : ‚àÄ {i j}, p i ‚Üí p j ‚Üí ‚àÉ k, p k ‚àß s k ‚äÜ s i ‚à© s j

namespace Filter

namespace IsBasis

/-- Constructs a filter basis from an indexed family of sets satisfying `IsBasis`. -/
protected def filterBasis {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set Œ±} (h : IsBasis p s) : FilterBasis Œ± where
  sets := { t | ‚àÉ i, p i ‚àß s i = t }
  nonempty :=
    let ‚ü®i, hi‚ü© := h.nonempty
    ‚ü®s i, ‚ü®i, hi, rfl‚ü©‚ü©
  inter_sets := by
    rintro _ _ ‚ü®i, hi, rfl‚ü© ‚ü®j, hj, rfl‚ü©
    rcases h.inter hi hj with ‚ü®k, hk, hk'‚ü©
    exact ‚ü®_, ‚ü®k, hk, rfl‚ü©, hk'‚ü©

variable {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set Œ±} (h : IsBasis p s)

theorem mem_filterBasis_iff {U : Set Œ±} : U ‚àà h.filterBasis ‚Üî ‚àÉ i, p i ‚àß s i = U :=
  Iff.rfl

end IsBasis

end Filter

namespace FilterBasis

/-- The filter associated to a filter basis. -/
protected def filter (B : FilterBasis Œ±) : Filter Œ± where
  sets := { s | ‚àÉ t ‚àà B, t ‚äÜ s }
  univ_sets := B.nonempty.imp fun s s_in => ‚ü®s_in, s.subset_univ‚ü©
  sets_of_superset := fun ‚ü®s, s_in, h‚ü© hxy => ‚ü®s, s_in, Set.Subset.trans h hxy‚ü©
  inter_sets := fun ‚ü®_s, s_in, hs‚ü© ‚ü®_t, t_in, ht‚ü© =>
    let ‚ü®u, u_in, u_sub‚ü© := B.inter_sets s_in t_in
    ‚ü®u, u_in, u_sub.trans (inter_subset_inter hs ht)‚ü©

theorem mem_filter_iff (B : FilterBasis Œ±) {U : Set Œ±} : U ‚àà B.filter ‚Üî ‚àÉ s ‚àà B, s ‚äÜ U :=
  Iff.rfl

theorem mem_filter_of_mem (B : FilterBasis Œ±) {U : Set Œ±} : U ‚àà B ‚Üí U ‚àà B.filter := fun U_in =>
  ‚ü®U, U_in, Subset.refl _‚ü©

end FilterBasis

namespace Filter

namespace IsBasis

variable {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set Œ±}

/-- Constructs a filter from an indexed family of sets satisfying `IsBasis`. -/
protected def filter (h : IsBasis p s) : Filter Œ± :=
  h.filterBasis.filter

protected theorem mem_filter_iff (h : IsBasis p s) {U : Set Œ±} :
    U ‚àà h.filter ‚Üî ‚àÉ i, p i ‚àß s i ‚äÜ U := by
  simp only [IsBasis.filter, FilterBasis.mem_filter_iff, mem_filterBasis_iff,
    exists_exists_and_eq_and]

end IsBasis

-- Porting note: was `protected` in Lean 3 but `protected` didn't work; removed
/-- We say that a filter `l` has a basis `s : Œπ ‚Üí Set Œ±` bounded by `p : Œπ ‚Üí Prop`,
if `t ‚àà l` if and only if `t` includes `s i` for some `i` such that `p i`. -/
structure HasBasis (l : Filter Œ±) (p : Œπ ‚Üí Prop) (s : Œπ ‚Üí Set Œ±) : Prop where
  /-- A set `t` belongs to a filter `l` iff it includes an element of the basis. -/
  mem_iff' : ‚àÄ t : Set Œ±, t ‚àà l ‚Üî ‚àÉ i, p i ‚àß s i ‚äÜ t

section SameType

variable {l l' : Filter Œ±} {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set Œ±} {t : Set Œ±} {i : Œπ} {p' : Œπ' ‚Üí Prop}
  {s' : Œπ' ‚Üí Set Œ±} {i' : Œπ'}

-- Porting note: use `‚àÉ i, p i ‚àß _` instead of `‚àÉ i (hi : p i), _`.
/-- Definition of `HasBasis` unfolded with implicit set argument. -/
theorem HasBasis.mem_iff (hl : l.HasBasis p s) : t ‚àà l ‚Üî ‚àÉ i, p i ‚àß s i ‚äÜ t :=
  hl.mem_iff' t

theorem HasBasis.eq_of_same_basis (hl : l.HasBasis p s) (hl' : l'.HasBasis p s) : l = l' := by
  ext t
  rw [hl.mem_iff, hl'.mem_iff]

-- Porting note: use `‚àÉ i, p i ‚àß _` instead of `‚àÉ i (hi : p i), _`.
theorem hasBasis_iff : l.HasBasis p s ‚Üî ‚àÄ t, t ‚àà l ‚Üî ‚àÉ i, p i ‚àß s i ‚äÜ t :=
  ‚ü®fun ‚ü®h‚ü© => h, fun h => ‚ü®h‚ü©‚ü©

theorem HasBasis.ex_mem (h : l.HasBasis p s) : ‚àÉ i, p i :=
  (h.mem_iff.mp univ_mem).imp fun _ => And.left

protected theorem HasBasis.nonempty (h : l.HasBasis p s) : Nonempty Œπ :=
  nonempty_of_exists h.ex_mem

protected theorem IsBasis.hasBasis (h : IsBasis p s) : HasBasis h.filter p s :=
  ‚ü®fun t => by simp only [h.mem_filter_iff, exists_prop]‚ü©

protected theorem HasBasis.mem_of_superset (hl : l.HasBasis p s) (hi : p i) (ht : s i ‚äÜ t) :
    t ‚àà l :=
  hl.mem_iff.2 ‚ü®i, hi, ht‚ü©

theorem HasBasis.mem_of_mem (hl : l.HasBasis p s) (hi : p i) : s i ‚àà l :=
  hl.mem_of_superset hi Subset.rfl

/-- Index of a basis set such that `s i ‚äÜ t` as an element of `Subtype p`. -/
noncomputable def HasBasis.index (h : l.HasBasis p s) (t : Set Œ±) (ht : t ‚àà l) : { i : Œπ // p i } :=
  ‚ü®(h.mem_iff.1 ht).choose, (h.mem_iff.1 ht).choose_spec.1‚ü©

theorem HasBasis.property_index (h : l.HasBasis p s) (ht : t ‚àà l) : p (h.index t ht) :=
  (h.index t ht).2

theorem HasBasis.set_index_mem (h : l.HasBasis p s) (ht : t ‚àà l) : s (h.index t ht) ‚àà l :=
  h.mem_of_mem <| h.property_index _

theorem HasBasis.set_index_subset (h : l.HasBasis p s) (ht : t ‚àà l) : s (h.index t ht) ‚äÜ t :=
  (h.mem_iff.1 ht).choose_spec.2

theorem HasBasis.isBasis (h : l.HasBasis p s) : IsBasis p s where
  nonempty := h.ex_mem
  inter hi hj := by
    simpa only [h.mem_iff] using inter_mem (h.mem_of_mem hi) (h.mem_of_mem hj)

theorem HasBasis.filter_eq (h : l.HasBasis p s) : h.isBasis.filter = l := by
  ext U
  simp [h.mem_iff, IsBasis.mem_filter_iff]

protected theorem _root_.FilterBasis.hasBasis (B : FilterBasis Œ±) :
    HasBasis B.filter (fun s : Set Œ± => s ‚àà B) id :=
  ‚ü®fun _ => B.mem_filter_iff‚ü©

theorem HasBasis.to_hasBasis' (hl : l.HasBasis p s) (h : ‚àÄ i, p i ‚Üí ‚àÉ i', p' i' ‚àß s' i' ‚äÜ s i)
    (h' : ‚àÄ i', p' i' ‚Üí s' i' ‚àà l) : l.HasBasis p' s' := by
  refine ‚ü®fun t => ‚ü®fun ht => ?_, fun ‚ü®i', hi', ht‚ü© => mem_of_superset (h' i' hi') ht‚ü©‚ü©
  rcases hl.mem_iff.1 ht with ‚ü®i, hi, ht‚ü©
  rcases h i hi with ‚ü®i', hi', hs's‚ü©
  exact ‚ü®i', hi', hs's.trans ht‚ü©

theorem HasBasis.to_hasBasis (hl : l.HasBasis p s) (h : ‚àÄ i, p i ‚Üí ‚àÉ i', p' i' ‚àß s' i' ‚äÜ s i)
    (h' : ‚àÄ i', p' i' ‚Üí ‚àÉ i, p i ‚àß s i ‚äÜ s' i') : l.HasBasis p' s' :=
  hl.to_hasBasis' h fun i' hi' =>
    let ‚ü®i, hi, hss'‚ü© := h' i' hi'
    hl.mem_iff.2 ‚ü®i, hi, hss'‚ü©

protected lemma HasBasis.congr (hl : l.HasBasis p s) {p' s'} (hp : ‚àÄ i, p i ‚Üî p' i)
    (hs : ‚àÄ i, p i ‚Üí s i = s' i) : l.HasBasis p' s' :=
  ‚ü®fun t ‚Ü¶ by simp only [hl.mem_iff, ‚Üê hp]; exact exists_congr fun i ‚Ü¶
    and_congr_right fun hi ‚Ü¶ hs i hi ‚ñ∏ Iff.rfl‚ü©

theorem HasBasis.to_subset (hl : l.HasBasis p s) {t : Œπ ‚Üí Set Œ±} (h : ‚àÄ i, p i ‚Üí t i ‚äÜ s i)
    (ht : ‚àÄ i, p i ‚Üí t i ‚àà l) : l.HasBasis p t :=
  hl.to_hasBasis' (fun i hi => ‚ü®i, hi, h i hi‚ü©) ht

theorem HasBasis.eventually_iff (hl : l.HasBasis p s) {q : Œ± ‚Üí Prop} :
    (‚àÄ·∂† x in l, q x) ‚Üî ‚àÉ i, p i ‚àß ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s i ‚Üí q x := by simpa using hl.mem_iff

theorem HasBasis.frequently_iff (hl : l.HasBasis p s) {q : Œ± ‚Üí Prop} :
    (‚àÉ·∂† x in l, q x) ‚Üî ‚àÄ i, p i ‚Üí ‚àÉ x ‚àà s i, q x := by
  simp only [Filter.Frequently, hl.eventually_iff]; push_neg; rfl

-- Porting note: use `‚àÉ i, p i ‚àß _` instead of `‚àÉ i (hi : p i), _`.
theorem HasBasis.exists_iff (hl : l.HasBasis p s) {P : Set Œ± ‚Üí Prop}
    (mono : ‚àÄ ‚¶És t‚¶Ñ, s ‚äÜ t ‚Üí P t ‚Üí P s) : (‚àÉ s ‚àà l, P s) ‚Üî ‚àÉ i, p i ‚àß P (s i) :=
  ‚ü®fun ‚ü®_s, hs, hP‚ü© =>
    let ‚ü®i, hi, his‚ü© := hl.mem_iff.1 hs
    ‚ü®i, hi, mono his hP‚ü©,
    fun ‚ü®i, hi, hP‚ü© => ‚ü®s i, hl.mem_of_mem hi, hP‚ü©‚ü©

theorem HasBasis.forall_iff (hl : l.HasBasis p s) {P : Set Œ± ‚Üí Prop}
    (mono : ‚àÄ ‚¶És t‚¶Ñ, s ‚äÜ t ‚Üí P s ‚Üí P t) : (‚àÄ s ‚àà l, P s) ‚Üî ‚àÄ i, p i ‚Üí P (s i) :=
  ‚ü®fun H i hi => H (s i) <| hl.mem_of_mem hi, fun H _s hs =>
    let ‚ü®i, hi, his‚ü© := hl.mem_iff.1 hs
    mono his (H i hi)‚ü©

theorem hasBasis_self {l : Filter Œ±} {P : Set Œ± ‚Üí Prop} :
    HasBasis l (fun s => s ‚àà l ‚àß P s) id ‚Üî ‚àÄ t ‚àà l, ‚àÉ r ‚àà l, P r ‚àß r ‚äÜ t := by
  simp only [hasBasis_iff, id, and_assoc]
  exact forall_congr' fun s =>
    ‚ü®fun h => h.1, fun h => ‚ü®h, fun ‚ü®t, hl, _, hts‚ü© => mem_of_superset hl hts‚ü©‚ü©

theorem HasBasis.comp_surjective (h : l.HasBasis p s) {g : Œπ' ‚Üí Œπ} (hg : Function.Surjective g) :
    l.HasBasis (p ‚àò g) (s ‚àò g) :=
  ‚ü®fun _ => h.mem_iff.trans hg.exists‚ü©

theorem HasBasis.comp_equiv (h : l.HasBasis p s) (e : Œπ' ‚âÉ Œπ) : l.HasBasis (p ‚àò e) (s ‚àò e) :=
  h.comp_surjective e.surjective

theorem HasBasis.to_image_id' (h : l.HasBasis p s) : l.HasBasis (fun t ‚Ü¶ ‚àÉ i, p i ‚àß s i = t) id :=
  ‚ü®fun _ ‚Ü¶ by simp [h.mem_iff]‚ü©

theorem HasBasis.to_image_id {Œπ : Type*} {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set Œ±} (h : l.HasBasis p s) :
    l.HasBasis (¬∑ ‚àà s '' {i | p i}) id :=
  h.to_image_id'

/-- If `{s i | p i}` is a basis of a filter `l` and each `s i` includes `s j` such that
`p j ‚àß q j`, then `{s j | p j ‚àß q j}` is a basis of `l`. -/
theorem HasBasis.restrict (h : l.HasBasis p s) {q : Œπ ‚Üí Prop}
    (hq : ‚àÄ i, p i ‚Üí ‚àÉ j, p j ‚àß q j ‚àß s j ‚äÜ s i) : l.HasBasis (fun i => p i ‚àß q i) s := by
  refine ‚ü®fun t => ‚ü®fun ht => ?_, fun ‚ü®i, hpi, hti‚ü© => h.mem_iff.2 ‚ü®i, hpi.1, hti‚ü©‚ü©‚ü©
  rcases h.mem_iff.1 ht with ‚ü®i, hpi, hti‚ü©
  rcases hq i hpi with ‚ü®j, hpj, hqj, hji‚ü©
  exact ‚ü®j, ‚ü®hpj, hqj‚ü©, hji.trans hti‚ü©

/-- If `{s i | p i}` is a basis of a filter `l` and `V ‚àà l`, then `{s i | p i ‚àß s i ‚äÜ V}`
is a basis of `l`. -/
theorem HasBasis.restrict_subset (h : l.HasBasis p s) {V : Set Œ±} (hV : V ‚àà l) :
    l.HasBasis (fun i => p i ‚àß s i ‚äÜ V) s :=
  h.restrict fun _i hi => (h.mem_iff.1 (inter_mem hV (h.mem_of_mem hi))).imp fun _j hj =>
    ‚ü®hj.1, subset_inter_iff.1 hj.2‚ü©

theorem HasBasis.hasBasis_self_subset {p : Set Œ± ‚Üí Prop} (h : l.HasBasis (fun s => s ‚àà l ‚àß p s) id)
    {V : Set Œ±} (hV : V ‚àà l) : l.HasBasis (fun s => s ‚àà l ‚àß p s ‚àß s ‚äÜ V) id := by
  simpa only [and_assoc] using h.restrict_subset hV

theorem HasBasis.ge_iff (hl' : l'.HasBasis p' s') : l ‚â§ l' ‚Üî ‚àÄ i', p' i' ‚Üí s' i' ‚àà l :=
  ‚ü®fun h _i' hi' => h <| hl'.mem_of_mem hi', fun h _s hs =>
    let ‚ü®_i', hi', hs‚ü© := hl'.mem_iff.1 hs
    mem_of_superset (h _ hi') hs‚ü©

-- Porting note: use `‚àÉ i, p i ‚àß _` instead of `‚àÉ i (hi : p i), _`.
theorem HasBasis.le_iff (hl : l.HasBasis p s) : l ‚â§ l' ‚Üî ‚àÄ t ‚àà l', ‚àÉ i, p i ‚àß s i ‚äÜ t := by
  simp only [le_def, hl.mem_iff]

-- Porting note: use `‚àÉ i, p i ‚àß _` instead of `‚àÉ i (hi : p i), _`.
theorem HasBasis.le_basis_iff (hl : l.HasBasis p s) (hl' : l'.HasBasis p' s') :
    l ‚â§ l' ‚Üî ‚àÄ i', p' i' ‚Üí ‚àÉ i, p i ‚àß s i ‚äÜ s' i' := by
  simp only [hl'.ge_iff, hl.mem_iff]

-- Porting note: use `‚àÉ i, p i ‚àß _` instead of `‚àÉ i (hi : p i), _`.
theorem HasBasis.ext (hl : l.HasBasis p s) (hl' : l'.HasBasis p' s')
    (h : ‚àÄ i, p i ‚Üí ‚àÉ i', p' i' ‚àß s' i' ‚äÜ s i) (h' : ‚àÄ i', p' i' ‚Üí ‚àÉ i, p i ‚àß s i ‚äÜ s' i') :
    l = l' := by
  apply le_antisymm
  ¬∑ rw [hl.le_basis_iff hl']
    simpa using h'
  ¬∑ rw [hl'.le_basis_iff hl]
    simpa using h

theorem hasBasis_principal (t : Set Œ±) : (ùìü t).HasBasis (fun _ : Unit => True) fun _ => t :=
  ‚ü®fun U => by simp‚ü©

theorem HasBasis.forall_mem_mem (h : HasBasis l p s) {x : Œ±} :
    (‚àÄ t ‚àà l, x ‚àà t) ‚Üî ‚àÄ i, p i ‚Üí x ‚àà s i := by
  simp only [h.mem_iff, exists_imp, and_imp]
  exact ‚ü®fun h i hi => h (s i) i hi Subset.rfl, fun h t i hi ht => ht (h i hi)‚ü©

variable {Œπ'' : Type*} [Preorder Œπ''] (l) (s'' : Œπ'' ‚Üí Set Œ±)

/-- `IsAntitoneBasis s` means the image of `s` is a filter basis such that `s` is decreasing. -/
structure IsAntitoneBasis : Prop extends IsBasis (fun _ => True) s'' where
  /-- The sequence of sets is antitone. -/
  protected antitone : Antitone s''

/-- We say that a filter `l` has an antitone basis `s : Œπ ‚Üí Set Œ±`, if `t ‚àà l` if and only if `t`
includes `s i` for some `i`, and `s` is decreasing. -/
structure HasAntitoneBasis (l : Filter Œ±) (s : Œπ'' ‚Üí Set Œ±) : Prop
    extends HasBasis l (fun _ => True) s where
  /-- The sequence of sets is antitone. -/
  protected antitone : Antitone s

end SameType

end Filter
