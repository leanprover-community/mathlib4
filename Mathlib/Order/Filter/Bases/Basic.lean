/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Johannes HÃ¶lzl, Mario Carneiro, Patrick Massot
-/
import Mathlib.Order.Filter.Bases.Defs
import Mathlib.Order.Filter.Map
import Mathlib.Data.Set.Sigma

/-!
# Basic results on filter bases

A filter basis `B : FilterBasis Î±` on a type `Î±` is a nonempty collection of sets of `Î±`
such that the intersection of two elements of this collection contains some element of
the collection.

## Main statements

* `Filter.basis_sets` : all sets of a filter form a basis;

* `Filter.HasBasis.inf`, `Filter.HasBasis.inf_principal`, `Filter.HasBasis.prod`,
  `Filter.HasBasis.prod_self`, `Filter.HasBasis.map`, `Filter.HasBasis.comap` : combinators to
  construct filters of `l âŠ“ l'`, `l âŠ“ ğ“Ÿ t`, `l Ã—Ë¢ l'`, `l Ã—Ë¢ l`, `l.map f`, `l.comap f`
  respectively;

* `Filter.HasBasis.tendsto_right_iff`, `Filter.HasBasis.tendsto_left_iff`,
  `Filter.HasBasis.tendsto_iff` : restate `Tendsto f l l'` in terms of bases.
-/

assert_not_exists Finset

open Set Filter

variable {Î± Î² Î³ : Type*} {Î¹ Î¹' : Sort*}

namespace FilterBasis

theorem eq_iInf_principal (B : FilterBasis Î±) : B.filter = â¨… s : B.sets, ğ“Ÿ s := by
  have : Directed (Â· â‰¥ Â·) fun s : B.sets => ğ“Ÿ (s : Set Î±) := by
    rintro âŸ¨U, U_inâŸ© âŸ¨V, V_inâŸ©
    rcases B.inter_sets U_in V_in with âŸ¨W, W_in, W_subâŸ©
    use âŸ¨W, W_inâŸ©
    simp only [le_principal_iff, mem_principal, Subtype.coe_mk]
    exact subset_inter_iff.mp W_sub
  ext U
  simp [mem_filter_iff, mem_iInf_of_directed this]

protected theorem generate (B : FilterBasis Î±) : generate B.sets = B.filter := by
  apply le_antisymm
  Â· intro U U_in
    rcases B.mem_filter_iff.mp U_in with âŸ¨V, V_in, hâŸ©
    exact GenerateSets.superset (GenerateSets.basic V_in) h
  Â· rw [le_generate_iff]
    apply mem_filter_of_mem

end FilterBasis

namespace Filter

namespace IsBasis

variable {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±}

theorem filter_eq_generate (h : IsBasis p s) : h.filter = generate { U | âˆƒ i, p i âˆ§ s i = U } := by
  rw [IsBasis.filter, â† h.filterBasis.generate, IsBasis.filterBasis]

end IsBasis

section SameType

variable {l l' : Filter Î±} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±} {t : Set Î±} {i : Î¹} {p' : Î¹' â†’ Prop}
  {s' : Î¹' â†’ Set Î±} {i' : Î¹'}

theorem HasBasis.eq_generate (h : l.HasBasis p s) : l = generate { U | âˆƒ i, p i âˆ§ s i = U } := by
  rw [â† h.isBasis.filter_eq_generate, h.filter_eq]

protected theorem HasBasis.neBot_iff (hl : l.HasBasis p s) :
    NeBot l â†” âˆ€ {i}, p i â†’ (s i).Nonempty :=
  forall_mem_nonempty_iff_neBot.symm.trans <| hl.forall_iff fun _ _ => Nonempty.mono

theorem HasBasis.eq_bot_iff (hl : l.HasBasis p s) : l = âŠ¥ â†” âˆƒ i, p i âˆ§ s i = âˆ… :=
  not_iff_not.1 <| neBot_iff.symm.trans <|
    hl.neBot_iff.trans <| by simp only [not_exists, not_and, nonempty_iff_ne_empty]

theorem basis_sets (l : Filter Î±) : l.HasBasis (fun s : Set Î± => s âˆˆ l) id :=
  âŸ¨fun _ => exists_mem_subset_iff.symmâŸ©

theorem asBasis_filter (f : Filter Î±) : f.asBasis.filter = f :=
  Filter.ext fun _ => exists_mem_subset_iff

theorem HasBasis.inf' (hl : l.HasBasis p s) (hl' : l'.HasBasis p' s') :
    (l âŠ“ l').HasBasis (fun i : PProd Î¹ Î¹' => p i.1 âˆ§ p' i.2) fun i => s i.1 âˆ© s' i.2 :=
  âŸ¨by
    intro t
    constructor
    Â· simp only [mem_inf_iff, hl.mem_iff, hl'.mem_iff]
      rintro âŸ¨t, âŸ¨i, hi, htâŸ©, t', âŸ¨i', hi', ht'âŸ©, rflâŸ©
      exact âŸ¨âŸ¨i, i'âŸ©, âŸ¨hi, hi'âŸ©, inter_subset_inter ht ht'âŸ©
    Â· rintro âŸ¨âŸ¨i, i'âŸ©, âŸ¨hi, hi'âŸ©, HâŸ©
      exact mem_inf_of_inter (hl.mem_of_mem hi) (hl'.mem_of_mem hi') HâŸ©

theorem HasBasis.inf {Î¹ Î¹' : Type*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±} {p' : Î¹' â†’ Prop}
    {s' : Î¹' â†’ Set Î±} (hl : l.HasBasis p s) (hl' : l'.HasBasis p' s') :
    (l âŠ“ l').HasBasis (fun i : Î¹ Ã— Î¹' => p i.1 âˆ§ p' i.2) fun i => s i.1 âˆ© s' i.2 :=
  (hl.inf' hl').comp_equiv Equiv.pprodEquivProd.symm

theorem hasBasis_iInf_of_directed' {Î¹ : Type*} {Î¹' : Î¹ â†’ Sort _} [Nonempty Î¹] {l : Î¹ â†’ Filter Î±}
    (s : âˆ€ i, Î¹' i â†’ Set Î±) (p : âˆ€ i, Î¹' i â†’ Prop) (hl : âˆ€ i, (l i).HasBasis (p i) (s i))
    (h : Directed (Â· â‰¥ Â·) l) :
    (â¨… i, l i).HasBasis (fun ii' : Î£i, Î¹' i => p ii'.1 ii'.2) fun ii' => s ii'.1 ii'.2 := by
  refine âŸ¨fun t => ?_âŸ©
  rw [mem_iInf_of_directed h, Sigma.exists]
  exact exists_congr fun i => (hl i).mem_iff

theorem hasBasis_iInf_of_directed {Î¹ : Type*} {Î¹' : Sort _} [Nonempty Î¹] {l : Î¹ â†’ Filter Î±}
    (s : Î¹ â†’ Î¹' â†’ Set Î±) (p : Î¹ â†’ Î¹' â†’ Prop) (hl : âˆ€ i, (l i).HasBasis (p i) (s i))
    (h : Directed (Â· â‰¥ Â·) l) :
    (â¨… i, l i).HasBasis (fun ii' : Î¹ Ã— Î¹' => p ii'.1 ii'.2) fun ii' => s ii'.1 ii'.2 := by
  refine âŸ¨fun t => ?_âŸ©
  rw [mem_iInf_of_directed h, Prod.exists]
  exact exists_congr fun i => (hl i).mem_iff

theorem hasBasis_biInf_of_directed' {Î¹ : Type*} {Î¹' : Î¹ â†’ Sort _} {dom : Set Î¹}
    (hdom : dom.Nonempty) {l : Î¹ â†’ Filter Î±} (s : âˆ€ i, Î¹' i â†’ Set Î±) (p : âˆ€ i, Î¹' i â†’ Prop)
    (hl : âˆ€ i âˆˆ dom, (l i).HasBasis (p i) (s i)) (h : DirectedOn (l â»Â¹'o GE.ge) dom) :
    (â¨… i âˆˆ dom, l i).HasBasis (fun ii' : Î£i, Î¹' i => ii'.1 âˆˆ dom âˆ§ p ii'.1 ii'.2) fun ii' =>
      s ii'.1 ii'.2 := by
  refine âŸ¨fun t => ?_âŸ©
  rw [mem_biInf_of_directed h hdom, Sigma.exists]
  refine exists_congr fun i => âŸ¨?_, ?_âŸ©
  Â· rintro âŸ¨hi, htiâŸ©
    rcases (hl i hi).mem_iff.mp hti with âŸ¨b, hb, hbtâŸ©
    exact âŸ¨b, âŸ¨hi, hbâŸ©, hbtâŸ©
  Â· rintro âŸ¨b, âŸ¨hi, hbâŸ©, hibtâŸ©
    exact âŸ¨hi, (hl i hi).mem_iff.mpr âŸ¨b, hb, hibtâŸ©âŸ©

theorem hasBasis_biInf_of_directed {Î¹ : Type*} {Î¹' : Sort _} {dom : Set Î¹} (hdom : dom.Nonempty)
    {l : Î¹ â†’ Filter Î±} (s : Î¹ â†’ Î¹' â†’ Set Î±) (p : Î¹ â†’ Î¹' â†’ Prop)
    (hl : âˆ€ i âˆˆ dom, (l i).HasBasis (p i) (s i)) (h : DirectedOn (l â»Â¹'o GE.ge) dom) :
    (â¨… i âˆˆ dom, l i).HasBasis (fun ii' : Î¹ Ã— Î¹' => ii'.1 âˆˆ dom âˆ§ p ii'.1 ii'.2) fun ii' =>
      s ii'.1 ii'.2 := by
  refine âŸ¨fun t => ?_âŸ©
  rw [mem_biInf_of_directed h hdom, Prod.exists]
  refine exists_congr fun i => âŸ¨?_, ?_âŸ©
  Â· rintro âŸ¨hi, htiâŸ©
    rcases (hl i hi).mem_iff.mp hti with âŸ¨b, hb, hbtâŸ©
    exact âŸ¨b, âŸ¨hi, hbâŸ©, hbtâŸ©
  Â· rintro âŸ¨b, âŸ¨hi, hbâŸ©, hibtâŸ©
    exact âŸ¨hi, (hl i hi).mem_iff.mpr âŸ¨b, hb, hibtâŸ©âŸ©

theorem hasBasis_pure (x : Î±) :
    (pure x : Filter Î±).HasBasis (fun _ : Unit => True) fun _ => {x} := by
  simp only [â† principal_singleton, hasBasis_principal]

theorem HasBasis.sup' (hl : l.HasBasis p s) (hl' : l'.HasBasis p' s') :
    (l âŠ” l').HasBasis (fun i : PProd Î¹ Î¹' => p i.1 âˆ§ p' i.2) fun i => s i.1 âˆª s' i.2 :=
  âŸ¨by
    intro t
    simp_rw [mem_sup, hl.mem_iff, hl'.mem_iff, PProd.exists, union_subset_iff,
       â† exists_and_right, â† exists_and_left]
    simp only [and_assoc, and_left_comm]âŸ©

theorem HasBasis.sup {Î¹ Î¹' : Type*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±} {p' : Î¹' â†’ Prop}
    {s' : Î¹' â†’ Set Î±} (hl : l.HasBasis p s) (hl' : l'.HasBasis p' s') :
    (l âŠ” l').HasBasis (fun i : Î¹ Ã— Î¹' => p i.1 âˆ§ p' i.2) fun i => s i.1 âˆª s' i.2 :=
  (hl.sup' hl').comp_equiv Equiv.pprodEquivProd.symm

theorem hasBasis_iSup {Î¹ : Sort*} {Î¹' : Î¹ â†’ Type*} {l : Î¹ â†’ Filter Î±} {p : âˆ€ i, Î¹' i â†’ Prop}
    {s : âˆ€ i, Î¹' i â†’ Set Î±} (hl : âˆ€ i, (l i).HasBasis (p i) (s i)) :
    (â¨† i, l i).HasBasis (fun f : âˆ€ i, Î¹' i => âˆ€ i, p i (f i)) fun f : âˆ€ i, Î¹' i => â‹ƒ i, s i (f i) :=
  hasBasis_iff.mpr fun t => by
    simp only [hasBasis_iff, (hl _).mem_iff, Classical.skolem, forall_and, iUnion_subset_iff,
      mem_iSup]

theorem HasBasis.sup_principal (hl : l.HasBasis p s) (t : Set Î±) :
    (l âŠ” ğ“Ÿ t).HasBasis p fun i => s i âˆª t :=
  âŸ¨fun u => by
    simp only [(hl.sup' (hasBasis_principal t)).mem_iff, PProd.exists, exists_prop, and_true,
      Unique.exists_iff]âŸ©

theorem HasBasis.sup_pure (hl : l.HasBasis p s) (x : Î±) :
    (l âŠ” pure x).HasBasis p fun i => s i âˆª {x} := by
  simp only [â† principal_singleton, hl.sup_principal]

theorem HasBasis.inf_principal (hl : l.HasBasis p s) (s' : Set Î±) :
    (l âŠ“ ğ“Ÿ s').HasBasis p fun i => s i âˆ© s' :=
  âŸ¨fun t => by
    simp only [mem_inf_principal, hl.mem_iff, subset_def, mem_setOf_eq, mem_inter_iff, and_imp]âŸ©

theorem HasBasis.principal_inf (hl : l.HasBasis p s) (s' : Set Î±) :
    (ğ“Ÿ s' âŠ“ l).HasBasis p fun i => s' âˆ© s i := by
  simpa only [inf_comm, inter_comm] using hl.inf_principal s'

theorem HasBasis.inf_basis_neBot_iff (hl : l.HasBasis p s) (hl' : l'.HasBasis p' s') :
    NeBot (l âŠ“ l') â†” âˆ€ â¦ƒiâ¦„, p i â†’ âˆ€ â¦ƒi'â¦„, p' i' â†’ (s i âˆ© s' i').Nonempty :=
  (hl.inf' hl').neBot_iff.trans <| by simp [@forall_swap _ Î¹']

theorem HasBasis.inf_neBot_iff (hl : l.HasBasis p s) :
    NeBot (l âŠ“ l') â†” âˆ€ â¦ƒiâ¦„, p i â†’ âˆ€ â¦ƒs'â¦„, s' âˆˆ l' â†’ (s i âˆ© s').Nonempty :=
  hl.inf_basis_neBot_iff l'.basis_sets

theorem HasBasis.inf_principal_neBot_iff (hl : l.HasBasis p s) {t : Set Î±} :
    NeBot (l âŠ“ ğ“Ÿ t) â†” âˆ€ â¦ƒiâ¦„, p i â†’ (s i âˆ© t).Nonempty :=
  (hl.inf_principal t).neBot_iff

-- Porting note: use `âˆƒ i, p i âˆ§ _` instead of `âˆƒ i (hi : p i), _`.
theorem HasBasis.disjoint_iff (hl : l.HasBasis p s) (hl' : l'.HasBasis p' s') :
    Disjoint l l' â†” âˆƒ i, p i âˆ§ âˆƒ i', p' i' âˆ§ Disjoint (s i) (s' i') :=
  not_iff_not.mp <| by simp only [_root_.disjoint_iff, â† Ne.eq_def, â† neBot_iff, inf_eq_inter,
    hl.inf_basis_neBot_iff hl', not_exists, not_and, bot_eq_empty, â† nonempty_iff_ne_empty]

-- Porting note: use `âˆƒ i, p i âˆ§ _` instead of `âˆƒ i (hi : p i), _`.
theorem _root_.Disjoint.exists_mem_filter_basis (h : Disjoint l l') (hl : l.HasBasis p s)
    (hl' : l'.HasBasis p' s') : âˆƒ i, p i âˆ§ âˆƒ i', p' i' âˆ§ Disjoint (s i) (s' i') :=
  (hl.disjoint_iff hl').1 h

theorem inf_neBot_iff :
    NeBot (l âŠ“ l') â†” âˆ€ â¦ƒs : Set Î±â¦„, s âˆˆ l â†’ âˆ€ â¦ƒs'â¦„, s' âˆˆ l' â†’ (s âˆ© s').Nonempty :=
  l.basis_sets.inf_neBot_iff

theorem inf_principal_neBot_iff {s : Set Î±} : NeBot (l âŠ“ ğ“Ÿ s) â†” âˆ€ U âˆˆ l, (U âˆ© s).Nonempty :=
  l.basis_sets.inf_principal_neBot_iff

theorem mem_iff_inf_principal_compl {f : Filter Î±} {s : Set Î±} : s âˆˆ f â†” f âŠ“ ğ“Ÿ sá¶œ = âŠ¥ := by
  refine not_iff_not.1 ((inf_principal_neBot_iff.trans ?_).symm.trans neBot_iff)
  exact
    âŸ¨fun h hs => by simpa [Set.not_nonempty_empty] using h s hs, fun hs t ht =>
      inter_compl_nonempty_iff.2 fun hts => hs <| mem_of_superset ht htsâŸ©

theorem not_mem_iff_inf_principal_compl {f : Filter Î±} {s : Set Î±} : s âˆ‰ f â†” NeBot (f âŠ“ ğ“Ÿ sá¶œ) :=
  (not_congr mem_iff_inf_principal_compl).trans neBot_iff.symm

@[simp]
theorem disjoint_principal_right {f : Filter Î±} {s : Set Î±} : Disjoint f (ğ“Ÿ s) â†” sá¶œ âˆˆ f := by
  rw [mem_iff_inf_principal_compl, compl_compl, disjoint_iff]

@[simp]
theorem disjoint_principal_left {f : Filter Î±} {s : Set Î±} : Disjoint (ğ“Ÿ s) f â†” sá¶œ âˆˆ f := by
  rw [disjoint_comm, disjoint_principal_right]

@[simp 1100] -- Porting note: higher priority for linter
theorem disjoint_principal_principal {s t : Set Î±} : Disjoint (ğ“Ÿ s) (ğ“Ÿ t) â†” Disjoint s t := by
  rw [â† subset_compl_iff_disjoint_left, disjoint_principal_left, mem_principal]

alias âŸ¨_, _root_.Disjoint.filter_principalâŸ© := disjoint_principal_principal

@[simp]
theorem disjoint_pure_pure {x y : Î±} : Disjoint (pure x : Filter Î±) (pure y) â†” x â‰  y := by
  simp only [â† principal_singleton, disjoint_principal_principal, disjoint_singleton]

-- Porting note: use `âˆƒ i, p i âˆ§ _` instead of `âˆƒ i (hi : p i), _`.
theorem HasBasis.disjoint_iff_left (h : l.HasBasis p s) :
    Disjoint l l' â†” âˆƒ i, p i âˆ§ (s i)á¶œ âˆˆ l' := by
  simp only [h.disjoint_iff l'.basis_sets, id, â† disjoint_principal_left,
    (hasBasis_principal _).disjoint_iff l'.basis_sets, true_and, Unique.exists_iff]

-- Porting note: use `âˆƒ i, p i âˆ§ _` instead of `âˆƒ i (hi : p i), _`.
theorem HasBasis.disjoint_iff_right (h : l.HasBasis p s) :
    Disjoint l' l â†” âˆƒ i, p i âˆ§ (s i)á¶œ âˆˆ l' :=
  disjoint_comm.trans h.disjoint_iff_left

theorem le_iff_forall_inf_principal_compl {f g : Filter Î±} : f â‰¤ g â†” âˆ€ V âˆˆ g, f âŠ“ ğ“Ÿ Vá¶œ = âŠ¥ :=
  forallâ‚‚_congr fun _ _ => mem_iff_inf_principal_compl

theorem inf_neBot_iff_frequently_left {f g : Filter Î±} :
    NeBot (f âŠ“ g) â†” âˆ€ {p : Î± â†’ Prop}, (âˆ€á¶  x in f, p x) â†’ âˆƒá¶  x in g, p x := by
  simp only [inf_neBot_iff, frequently_iff, and_comm]; rfl

theorem inf_neBot_iff_frequently_right {f g : Filter Î±} :
    NeBot (f âŠ“ g) â†” âˆ€ {p : Î± â†’ Prop}, (âˆ€á¶  x in g, p x) â†’ âˆƒá¶  x in f, p x := by
  rw [inf_comm]
  exact inf_neBot_iff_frequently_left

theorem HasBasis.eq_biInf (h : l.HasBasis p s) : l = â¨… (i) (_ : p i), ğ“Ÿ (s i) :=
  eq_biInf_of_mem_iff_exists_mem fun {_} => by simp only [h.mem_iff, mem_principal, exists_prop]

theorem HasBasis.eq_iInf (h : l.HasBasis (fun _ => True) s) : l = â¨… i, ğ“Ÿ (s i) := by
  simpa only [iInf_true] using h.eq_biInf

theorem hasBasis_iInf_principal {s : Î¹ â†’ Set Î±} (h : Directed (Â· â‰¥ Â·) s) [Nonempty Î¹] :
    (â¨… i, ğ“Ÿ (s i)).HasBasis (fun _ => True) s :=
  âŸ¨fun t => by
    simpa only [true_and] using mem_iInf_of_directed (h.mono_comp _ monotone_principal.dual) tâŸ©

theorem hasBasis_biInf_principal {s : Î² â†’ Set Î±} {S : Set Î²} (h : DirectedOn (s â»Â¹'o (Â· â‰¥ Â·)) S)
    (ne : S.Nonempty) : (â¨… i âˆˆ S, ğ“Ÿ (s i)).HasBasis (fun i => i âˆˆ S) s :=
  âŸ¨fun t => by
    refine mem_biInf_of_directed ?_ ne
    rw [directedOn_iff_directed, â† directed_comp] at h âŠ¢
    refine h.mono_comp _ ?_
    exact fun _ _ => principal_mono.2âŸ©

theorem hasBasis_biInf_principal' {Î¹ : Type*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±}
    (h : âˆ€ i, p i â†’ âˆ€ j, p j â†’ âˆƒ k, p k âˆ§ s k âŠ† s i âˆ§ s k âŠ† s j) (ne : âˆƒ i, p i) :
    (â¨… (i) (_ : p i), ğ“Ÿ (s i)).HasBasis p s :=
  Filter.hasBasis_biInf_principal h ne

theorem HasBasis.map (f : Î± â†’ Î²) (hl : l.HasBasis p s) : (l.map f).HasBasis p fun i => f '' s i :=
  âŸ¨fun t => by simp only [mem_map, image_subset_iff, hl.mem_iff, preimage]âŸ©

theorem HasBasis.comap (f : Î² â†’ Î±) (hl : l.HasBasis p s) :
    (l.comap f).HasBasis p fun i => f â»Â¹' s i :=
  âŸ¨fun t => by
    simp only [mem_comap', hl.mem_iff]
    refine exists_congr (fun i => Iff.rfl.and ?_)
    exact âŸ¨fun h x hx => h hx rfl, fun h y hy x hx => h <| by rwa [mem_preimage, hx]âŸ©âŸ©

theorem comap_hasBasis (f : Î± â†’ Î²) (l : Filter Î²) :
    HasBasis (comap f l) (fun s : Set Î² => s âˆˆ l) fun s => f â»Â¹' s :=
  âŸ¨fun _ => mem_comapâŸ©

protected theorem HasBasis.biInf_mem [CompleteLattice Î²] {f : Set Î± â†’ Î²} (h : HasBasis l p s)
    (hf : Monotone f) : â¨… t âˆˆ l, f t = â¨… (i) (_ : p i), f (s i) :=
  le_antisymm (le_iInfâ‚‚ fun i hi => iInfâ‚‚_le (s i) (h.mem_of_mem hi)) <|
    le_iInfâ‚‚ fun _t ht =>
      let âŸ¨i, hpi, hiâŸ© := h.mem_iff.1 ht
      iInfâ‚‚_le_of_le i hpi (hf hi)

protected theorem HasBasis.biInter_mem {f : Set Î± â†’ Set Î²} (h : HasBasis l p s) (hf : Monotone f) :
    â‹‚ t âˆˆ l, f t = â‹‚ (i) (_ : p i), f (s i) :=
  h.biInf_mem hf

protected theorem HasBasis.ker (h : HasBasis l p s) : l.ker = â‹‚ (i) (_ : p i), s i :=
  sInter_eq_biInter.trans <| h.biInter_mem monotone_id

variable {Î¹'' : Type*} [Preorder Î¹''] (l) (s'' : Î¹'' â†’ Set Î±)

protected theorem HasAntitoneBasis.map {l : Filter Î±} {s : Î¹'' â†’ Set Î±}
    (hf : HasAntitoneBasis l s) (m : Î± â†’ Î²) : HasAntitoneBasis (map m l) (m '' s Â·) :=
  âŸ¨HasBasis.map _ hf.toHasBasis, fun _ _ h => image_subset _ <| hf.2 hâŸ©

protected theorem HasAntitoneBasis.comap {l : Filter Î±} {s : Î¹'' â†’ Set Î±}
    (hf : HasAntitoneBasis l s) (m : Î² â†’ Î±) : HasAntitoneBasis (comap m l) (m â»Â¹' s Â·) :=
  âŸ¨hf.1.comap _, fun _ _ h â†¦ preimage_mono (hf.2 h)âŸ©

lemma HasAntitoneBasis.iInf_principal {Î¹ : Type*} [Preorder Î¹] [Nonempty Î¹] [IsDirected Î¹ (Â· â‰¤ Â·)]
    {s : Î¹ â†’ Set Î±} (hs : Antitone s) : (â¨… i, ğ“Ÿ (s i)).HasAntitoneBasis s :=
  âŸ¨hasBasis_iInf_principal hs.directed_ge, hsâŸ©

end SameType

section TwoTypes

variable {la : Filter Î±} {pa : Î¹ â†’ Prop} {sa : Î¹ â†’ Set Î±} {lb : Filter Î²} {pb : Î¹' â†’ Prop}
  {sb : Î¹' â†’ Set Î²} {f : Î± â†’ Î²}

-- Porting note: use `âˆƒ i, p i âˆ§ _` instead of `âˆƒ i (hi : p i), _`.
theorem HasBasis.tendsto_left_iff (hla : la.HasBasis pa sa) :
    Tendsto f la lb â†” âˆ€ t âˆˆ lb, âˆƒ i, pa i âˆ§ MapsTo f (sa i) t := by
  simp only [Tendsto, (hla.map f).le_iff, image_subset_iff]
  rfl

theorem HasBasis.tendsto_right_iff (hlb : lb.HasBasis pb sb) :
    Tendsto f la lb â†” âˆ€ i, pb i â†’ âˆ€á¶  x in la, f x âˆˆ sb i := by
  simp only [Tendsto, hlb.ge_iff, mem_map', Filter.Eventually]

-- Porting note: use `âˆƒ i, p i âˆ§ _` instead of `âˆƒ i (hi : p i), _`.
theorem HasBasis.tendsto_iff (hla : la.HasBasis pa sa) (hlb : lb.HasBasis pb sb) :
    Tendsto f la lb â†” âˆ€ ib, pb ib â†’ âˆƒ ia, pa ia âˆ§ âˆ€ x âˆˆ sa ia, f x âˆˆ sb ib := by
  simp [hlb.tendsto_right_iff, hla.eventually_iff]

-- Porting note: use `âˆƒ i, p i âˆ§ _` instead of `âˆƒ i (hi : p i), _`.
theorem Tendsto.basis_left (H : Tendsto f la lb) (hla : la.HasBasis pa sa) :
    âˆ€ t âˆˆ lb, âˆƒ i, pa i âˆ§ MapsTo f (sa i) t :=
  hla.tendsto_left_iff.1 H

theorem Tendsto.basis_right (H : Tendsto f la lb) (hlb : lb.HasBasis pb sb) :
    âˆ€ i, pb i â†’ âˆ€á¶  x in la, f x âˆˆ sb i :=
  hlb.tendsto_right_iff.1 H

-- Porting note: use `âˆƒ i, p i âˆ§ _` instead of `âˆƒ i (hi : p i), _`.
theorem Tendsto.basis_both (H : Tendsto f la lb) (hla : la.HasBasis pa sa)
    (hlb : lb.HasBasis pb sb) :
    âˆ€ ib, pb ib â†’ âˆƒ ia, pa ia âˆ§ MapsTo f (sa ia) (sb ib) :=
  (hla.tendsto_iff hlb).1 H

theorem HasBasis.prod_pprod (hla : la.HasBasis pa sa) (hlb : lb.HasBasis pb sb) :
    (la Ã—Ë¢ lb).HasBasis (fun i : PProd Î¹ Î¹' => pa i.1 âˆ§ pb i.2) fun i => sa i.1 Ã—Ë¢ sb i.2 :=
  (hla.comap Prod.fst).inf' (hlb.comap Prod.snd)

theorem HasBasis.prod {Î¹ Î¹' : Type*} {pa : Î¹ â†’ Prop} {sa : Î¹ â†’ Set Î±} {pb : Î¹' â†’ Prop}
    {sb : Î¹' â†’ Set Î²} (hla : la.HasBasis pa sa) (hlb : lb.HasBasis pb sb) :
    (la Ã—Ë¢ lb).HasBasis (fun i : Î¹ Ã— Î¹' => pa i.1 âˆ§ pb i.2) fun i => sa i.1 Ã—Ë¢ sb i.2 :=
  (hla.comap Prod.fst).inf (hlb.comap Prod.snd)

theorem HasBasis.prod_same_index {p : Î¹ â†’ Prop} {sb : Î¹ â†’ Set Î²} (hla : la.HasBasis p sa)
    (hlb : lb.HasBasis p sb) (h_dir : âˆ€ {i j}, p i â†’ p j â†’ âˆƒ k, p k âˆ§ sa k âŠ† sa i âˆ§ sb k âŠ† sb j) :
    (la Ã—Ë¢ lb).HasBasis p fun i => sa i Ã—Ë¢ sb i := by
  simp only [hasBasis_iff, (hla.prod_pprod hlb).mem_iff]
  refine fun t => âŸ¨?_, ?_âŸ©
  Â· rintro âŸ¨âŸ¨i, jâŸ©, âŸ¨hi, hjâŸ©, hsub : sa i Ã—Ë¢ sb j âŠ† tâŸ©
    rcases h_dir hi hj with âŸ¨k, hk, ki, kjâŸ©
    exact âŸ¨k, hk, (Set.prod_mono ki kj).trans hsubâŸ©
  Â· rintro âŸ¨i, hi, hâŸ©
    exact âŸ¨âŸ¨i, iâŸ©, âŸ¨hi, hiâŸ©, hâŸ©

theorem HasBasis.prod_same_index_mono {Î¹ : Type*} [LinearOrder Î¹] {p : Î¹ â†’ Prop} {sa : Î¹ â†’ Set Î±}
    {sb : Î¹ â†’ Set Î²} (hla : la.HasBasis p sa) (hlb : lb.HasBasis p sb)
    (hsa : MonotoneOn sa { i | p i }) (hsb : MonotoneOn sb { i | p i }) :
    (la Ã—Ë¢ lb).HasBasis p fun i => sa i Ã—Ë¢ sb i :=
  hla.prod_same_index hlb fun {i j} hi hj =>
    have : p (min i j) := min_rec' _ hi hj
    âŸ¨min i j, this, hsa this hi <| min_le_left _ _, hsb this hj <| min_le_right _ _âŸ©

theorem HasBasis.prod_same_index_anti {Î¹ : Type*} [LinearOrder Î¹] {p : Î¹ â†’ Prop} {sa : Î¹ â†’ Set Î±}
    {sb : Î¹ â†’ Set Î²} (hla : la.HasBasis p sa) (hlb : lb.HasBasis p sb)
    (hsa : AntitoneOn sa { i | p i }) (hsb : AntitoneOn sb { i | p i }) :
    (la Ã—Ë¢ lb).HasBasis p fun i => sa i Ã—Ë¢ sb i :=
  @HasBasis.prod_same_index_mono _ _ _ _ Î¹áµ’áµˆ _ _ _ _ hla hlb hsa.dual_left hsb.dual_left

theorem HasBasis.prod_self (hl : la.HasBasis pa sa) :
    (la Ã—Ë¢ la).HasBasis pa fun i => sa i Ã—Ë¢ sa i :=
  hl.prod_same_index hl fun {i j} hi hj => by
    simpa only [exists_prop, subset_inter_iff] using
      hl.mem_iff.1 (inter_mem (hl.mem_of_mem hi) (hl.mem_of_mem hj))

theorem mem_prod_self_iff {s} : s âˆˆ la Ã—Ë¢ la â†” âˆƒ t âˆˆ la, t Ã—Ë¢ t âŠ† s :=
  la.basis_sets.prod_self.mem_iff

lemma eventually_prod_self_iff {r : Î± â†’ Î± â†’ Prop} :
    (âˆ€á¶  x in la Ã—Ë¢ la, r x.1 x.2) â†” âˆƒ t âˆˆ la, âˆ€ x âˆˆ t, âˆ€ y âˆˆ t, r x y :=
  mem_prod_self_iff.trans <| by simp only [prod_subset_iff, mem_setOf_eq]

/-- A version of `eventually_prod_self_iff` that is more suitable for forward rewriting. -/
lemma eventually_prod_self_iff' {r : Î± Ã— Î± â†’ Prop} :
    (âˆ€á¶  x in la Ã—Ë¢ la, r x) â†” âˆƒ t âˆˆ la, âˆ€ x âˆˆ t, âˆ€ y âˆˆ t, r (x, y) :=
  Iff.symm eventually_prod_self_iff.symm

theorem HasAntitoneBasis.prod {Î¹ : Type*} [LinearOrder Î¹] {f : Filter Î±} {g : Filter Î²}
    {s : Î¹ â†’ Set Î±} {t : Î¹ â†’ Set Î²} (hf : HasAntitoneBasis f s) (hg : HasAntitoneBasis g t) :
    HasAntitoneBasis (f Ã—Ë¢ g) fun n => s n Ã—Ë¢ t n :=
  âŸ¨hf.1.prod_same_index_anti hg.1 (hf.2.antitoneOn _) (hg.2.antitoneOn _), hf.2.set_prod hg.2âŸ©

theorem HasBasis.coprod {Î¹ Î¹' : Type*} {pa : Î¹ â†’ Prop} {sa : Î¹ â†’ Set Î±} {pb : Î¹' â†’ Prop}
    {sb : Î¹' â†’ Set Î²} (hla : la.HasBasis pa sa) (hlb : lb.HasBasis pb sb) :
    (la.coprod lb).HasBasis (fun i : Î¹ Ã— Î¹' => pa i.1 âˆ§ pb i.2) fun i =>
      Prod.fst â»Â¹' sa i.1 âˆª Prod.snd â»Â¹' sb i.2 :=
  (hla.comap Prod.fst).sup (hlb.comap Prod.snd)

end TwoTypes

theorem map_sigma_mk_comap {Ï€ : Î± â†’ Type*} {Ï€' : Î² â†’ Type*} {f : Î± â†’ Î²}
    (hf : Function.Injective f) (g : âˆ€ a, Ï€ a â†’ Ï€' (f a)) (a : Î±) (l : Filter (Ï€' (f a))) :
    map (Sigma.mk a) (comap (g a) l) = comap (Sigma.map f g) (map (Sigma.mk (f a)) l) := by
  refine (((basis_sets _).comap _).map _).eq_of_same_basis ?_
  convert ((basis_sets l).map (Sigma.mk (f a))).comap (Sigma.map f g)
  apply image_sigmaMk_preimage_sigmaMap hf

end Filter
