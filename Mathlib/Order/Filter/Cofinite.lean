/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Jeremy Avigad, Yury Kudryashov
-/
module

public import Mathlib.Data.Finite.Prod
public import Mathlib.Data.Fintype.Pi
public import Mathlib.Data.Set.Finite.Lemmas
public import Mathlib.Order.ConditionallyCompleteLattice.Basic
public import Mathlib.Order.Filter.CountablyGenerated
public import Mathlib.Order.Filter.Ker
public import Mathlib.Order.Filter.Pi
public import Mathlib.Order.Filter.Prod
public import Mathlib.Order.Filter.AtTopBot.Basic

/-!
# The cofinite filter

In this file we define

`Filter.cofinite`: the filter of sets with finite complement

and prove its basic properties. In particular, we prove that for `â„•` it is equal to `Filter.atTop`.

## TODO

Define filters for other cardinalities of the complement.
-/

@[expose] public section

open Set Function

variable {Î¹ Î± Î² : Type*} {l : Filter Î±}

namespace Filter

/-- The cofinite filter is the filter of subsets whose complements are finite. -/
def cofinite : Filter Î± :=
  comk Set.Finite finite_empty (fun _t ht _s hsub â†¦ ht.subset hsub) fun _ h _ â†¦ h.union

@[simp]
theorem mem_cofinite {s : Set Î±} : s âˆˆ @cofinite Î± â†” sá¶œ.Finite :=
  Iff.rfl

@[simp]
theorem eventually_cofinite {p : Î± â†’ Prop} : (âˆ€á¶  x in cofinite, p x) â†” { x | Â¬p x }.Finite :=
  Iff.rfl

theorem hasBasis_cofinite : HasBasis cofinite (fun s : Set Î± => s.Finite) compl :=
  âŸ¨fun s =>
    âŸ¨fun h => âŸ¨sá¶œ, h, (compl_compl s).subsetâŸ©, fun âŸ¨_t, htf, htsâŸ© =>
      htf.subset <| compl_subset_comm.2 htsâŸ©âŸ©

instance cofinite_neBot [Infinite Î±] : NeBot (@cofinite Î±) :=
  hasBasis_cofinite.neBot_iff.2 fun hs => hs.infinite_compl.nonempty

@[simp]
theorem cofinite_eq_bot_iff : @cofinite Î± = âŠ¥ â†” Finite Î± := by
  simp [â† empty_mem_iff_bot, finite_univ_iff]

@[simp]
theorem cofinite_eq_bot [Finite Î±] : @cofinite Î± = âŠ¥ := cofinite_eq_bot_iff.2 â€¹_â€º

theorem frequently_cofinite_iff_infinite {p : Î± â†’ Prop} :
    (âˆƒá¶  x in cofinite, p x) â†” Set.Infinite { x | p x } := by
  simp only [Filter.Frequently, eventually_cofinite, not_not, Set.Infinite]

lemma frequently_cofinite_mem_iff_infinite {s : Set Î±} : (âˆƒá¶  x in cofinite, x âˆˆ s) â†” s.Infinite :=
  frequently_cofinite_iff_infinite

alias âŸ¨_, _root_.Set.Infinite.frequently_cofiniteâŸ© := frequently_cofinite_mem_iff_infinite

@[simp]
lemma cofinite_inf_principal_neBot_iff {s : Set Î±} : (cofinite âŠ“ ğ“Ÿ s).NeBot â†” s.Infinite :=
  frequently_mem_iff_neBot.symm.trans frequently_cofinite_mem_iff_infinite

alias âŸ¨_, _root_.Set.Infinite.cofinite_inf_principal_neBotâŸ© := cofinite_inf_principal_neBot_iff

theorem _root_.Set.Finite.compl_mem_cofinite {s : Set Î±} (hs : s.Finite) : sá¶œ âˆˆ @cofinite Î± :=
  mem_cofinite.2 <| (compl_compl s).symm â–¸ hs

theorem _root_.Set.Finite.eventually_cofinite_notMem {s : Set Î±} (hs : s.Finite) :
    âˆ€á¶  x in cofinite, x âˆ‰ s :=
  hs.compl_mem_cofinite

@[deprecated (since := "2025-05-24")]
alias _root_.Set.Finite.eventually_cofinite_nmem := _root_.Set.Finite.eventually_cofinite_notMem

theorem _root_.Finset.eventually_cofinite_notMem (s : Finset Î±) : âˆ€á¶  x in cofinite, x âˆ‰ s :=
  s.finite_toSet.eventually_cofinite_notMem

@[deprecated (since := "2025-05-24")]
alias _root_.Finset.eventually_cofinite_nmem := _root_.Finset.eventually_cofinite_notMem

theorem _root_.Set.infinite_iff_frequently_cofinite {s : Set Î±} :
    Set.Infinite s â†” âˆƒá¶  x in cofinite, x âˆˆ s :=
  frequently_cofinite_iff_infinite.symm

theorem eventually_cofinite_ne (x : Î±) : âˆ€á¶  a in cofinite, a â‰  x :=
  (Set.finite_singleton x).eventually_cofinite_notMem

theorem le_cofinite_iff_compl_singleton_mem : l â‰¤ cofinite â†” âˆ€ x, {x}á¶œ âˆˆ l := by
  refine âŸ¨fun h x => h (finite_singleton x).compl_mem_cofinite, fun h s (hs : sá¶œ.Finite) => ?_âŸ©
  rw [â† compl_compl s, â† biUnion_of_singleton sá¶œ, compl_iUnionâ‚‚, Filter.biInter_mem hs]
  exact fun x _ => h x

theorem le_cofinite_iff_eventually_ne : l â‰¤ cofinite â†” âˆ€ x, âˆ€á¶  y in l, y â‰  x :=
  le_cofinite_iff_compl_singleton_mem

/-- If `Î±` is a preorder with no top element, then `atTop â‰¤ cofinite`. -/
theorem atTop_le_cofinite [Preorder Î±] [NoTopOrder Î±] : (atTop : Filter Î±) â‰¤ cofinite :=
  le_cofinite_iff_eventually_ne.mpr eventually_ne_atTop

/-- If `Î±` is a preorder with no bottom element, then `atBot â‰¤ cofinite`. -/
theorem atBot_le_cofinite [Preorder Î±] [NoBotOrder Î±] : (atBot : Filter Î±) â‰¤ cofinite :=
  le_cofinite_iff_eventually_ne.mpr eventually_ne_atBot

theorem comap_cofinite_le (f : Î± â†’ Î²) : comap f cofinite â‰¤ cofinite :=
  le_cofinite_iff_eventually_ne.mpr fun x =>
    mem_comap.2 âŸ¨{f x}á¶œ, (finite_singleton _).compl_mem_cofinite, fun _ => ne_of_apply_ne fâŸ©

/-- The coproduct of the cofinite filters on two types is the cofinite filter on their product. -/
theorem coprod_cofinite : (cofinite : Filter Î±).coprod (cofinite : Filter Î²) = cofinite :=
  Filter.coext fun s => by
    simp only [compl_mem_coprod, mem_cofinite, compl_compl, finite_image_fst_and_snd_iff]

theorem coprodáµ¢_cofinite {Î± : Î¹ â†’ Type*} [Finite Î¹] :
    (Filter.coprodáµ¢ fun i => (cofinite : Filter (Î± i))) = cofinite :=
  Filter.coext fun s => by
    simp only [compl_mem_coprodáµ¢, mem_cofinite, compl_compl, forall_finite_image_eval_iff]

theorem disjoint_cofinite_left : Disjoint cofinite l â†” âˆƒ s âˆˆ l, Set.Finite s := by
  simp [l.basis_sets.disjoint_iff_right]

theorem disjoint_cofinite_right : Disjoint l cofinite â†” âˆƒ s âˆˆ l, Set.Finite s :=
  disjoint_comm.trans disjoint_cofinite_left

/-- If `l â‰¥ Filter.cofinite` is a countably generated filter, then `l.ker` is cocountable. -/
theorem countable_compl_ker [l.IsCountablyGenerated] (h : cofinite â‰¤ l) : Set.Countable l.kerá¶œ := by
  rcases exists_antitone_basis l with âŸ¨s, hsâŸ©
  simp only [hs.ker, iInter_true, compl_iInter]
  exact countable_iUnion fun n â†¦ Set.Finite.countable <| h <| hs.mem _

/-- If `f` tends to a countably generated filter `l` along `Filter.cofinite`,
then for all but countably many elements, `f x âˆˆ l.ker`. -/
theorem Tendsto.countable_compl_preimage_ker {f : Î± â†’ Î²}
    {l : Filter Î²} [l.IsCountablyGenerated] (h : Tendsto f cofinite l) :
    Set.Countable (f â»Â¹' l.ker)á¶œ := by rw [â† ker_comap]; exact countable_compl_ker h.le_comap

/-- Given a collection of filters `l i : Filter (Î± i)` and sets `s i âˆˆ l i`,
if all but finitely many of `s i` are the whole space,
then their indexed product `Set.pi Set.univ s` belongs to the filter `Filter.pi l`. -/
theorem univ_pi_mem_pi {Î± : Î¹ â†’ Type*} {s : âˆ€ i, Set (Î± i)} {l : âˆ€ i, Filter (Î± i)}
    (h : âˆ€ i, s i âˆˆ l i) (hfin : âˆ€á¶  i in cofinite, s i = univ) : univ.pi s âˆˆ pi l := by
  filter_upwards [pi_mem_pi hfin fun i _ â†¦ h i] with a ha i _
  if hi : s i = univ then
    simp [hi]
  else
    exact ha i hi

/-- Given a family of maps `f i : Î± i â†’ Î² i` and a family of filters `l i : Filter (Î± i)`,
if all but finitely many of `f i` are surjective,
then the indexed product of `f i`s maps the indexed product of the filters `l i`
to the indexed products of their pushforwards under individual `f i`s.

See also `map_piMap_pi_finite` for the case of a finite index type.
-/
theorem map_piMap_pi {Î± Î² : Î¹ â†’ Type*} {f : âˆ€ i, Î± i â†’ Î² i}
    (hf : âˆ€á¶  i in cofinite, Surjective (f i)) (l : âˆ€ i, Filter (Î± i)) :
    map (Pi.map f) (pi l) = pi fun i â†¦ map (f i) (l i) := by
  refine le_antisymm (tendsto_piMap_pi fun _ â†¦ tendsto_map) ?_
  refine ((hasBasis_pi fun i â†¦ (l i).basis_sets).map _).ge_iff.2 ?_
  rintro âŸ¨I, sâŸ© âŸ¨hI : I.Finite, hs : âˆ€ i âˆˆ I, s i âˆˆ l iâŸ©
  classical
  rw [â† univ_pi_piecewise_univ, piMap_image_univ_pi]
  refine univ_pi_mem_pi (fun i â†¦ ?_) ?_
  Â· by_cases hi : i âˆˆ I
    Â· simpa [hi] using image_mem_map (hs i hi)
    Â· simp [hi]
  Â· filter_upwards [hf, hI.compl_mem_cofinite] with i hsurj (hiI : i âˆ‰ I)
    simp [hiI, hsurj.range_eq]

/-- Given finite families of maps `f i : Î± i â†’ Î² i` and of filters `l i : Filter (Î± i)`,
the indexed product of `f i`s maps the indexed product of the filters `l i`
to the indexed products of their pushforwards under individual `f i`s.

See also `map_piMap_pi` for a more general case.
-/
theorem map_piMap_pi_finite {Î± Î² : Î¹ â†’ Type*} [Finite Î¹]
    (f : âˆ€ i, Î± i â†’ Î² i) (l : âˆ€ i, Filter (Î± i)) :
    map (Pi.map f) (pi l) = pi fun i â†¦ map (f i) (l i) :=
  map_piMap_pi (by simp) l

end Filter

open Filter

lemma Set.Finite.cofinite_inf_principal_compl {s : Set Î±} (hs : s.Finite) :
    cofinite âŠ“ ğ“Ÿ sá¶œ = cofinite := by
  simpa using hs.compl_mem_cofinite

lemma Set.Finite.cofinite_inf_principal_diff {s t : Set Î±} (ht : t.Finite) :
    cofinite âŠ“ ğ“Ÿ (s \ t) = cofinite âŠ“ ğ“Ÿ s := by
  rw [diff_eq, â† inf_principal, â† inf_assoc, inf_right_comm, ht.cofinite_inf_principal_compl]

/-- For natural numbers the filters `Filter.cofinite` and `Filter.atTop` coincide. -/
theorem Nat.cofinite_eq_atTop : @cofinite â„• = atTop := by
  refine le_antisymm ?_ atTop_le_cofinite
  refine atTop_basis.ge_iff.2 fun N _ => ?_
  simpa only [mem_cofinite, compl_Ici] using finite_lt_nat N

theorem Nat.frequently_atTop_iff_infinite {p : â„• â†’ Prop} :
    (âˆƒá¶  n in atTop, p n) â†” Set.Infinite { n | p n } := by
  rw [â† Nat.cofinite_eq_atTop, frequently_cofinite_iff_infinite]

lemma Nat.eventually_pos : âˆ€á¶  (k : â„•) in Filter.atTop, 0 < k :=
  Filter.eventually_of_mem (Filter.mem_atTop_sets.mpr âŸ¨1, fun _ hx â†¦ hxâŸ©) (fun _ hx â†¦ hx)

theorem Filter.Tendsto.exists_within_forall_le {Î± Î² : Type*} [LinearOrder Î²] {s : Set Î±}
    (hs : s.Nonempty) {f : Î± â†’ Î²} (hf : Filter.Tendsto f Filter.cofinite Filter.atTop) :
    âˆƒ aâ‚€ âˆˆ s, âˆ€ a âˆˆ s, f aâ‚€ â‰¤ f a := by
  by_cases! all_top : âˆƒ y âˆˆ s, âˆƒ x, f y < x
  Â· -- the set of points `{y | f y < x}` is nonempty and finite, so we take `min` over this set
    rcases all_top with âŸ¨y, hys, x, hxâŸ©
    have : { y | Â¬x â‰¤ f y }.Finite := Filter.eventually_cofinite.mp (tendsto_atTop.1 hf x)
    simp only [not_le] at this
    obtain âŸ¨aâ‚€, âŸ¨haâ‚€ : f aâ‚€ < x, haâ‚€sâŸ©, others_biggerâŸ© :=
      exists_min_image _ f (this.inter_of_left s) âŸ¨y, hx, hysâŸ©
    refine âŸ¨aâ‚€, haâ‚€s, fun a has => (lt_or_ge (f a) x).elim ?_ (le_trans haâ‚€.le)âŸ©
    exact fun h => others_bigger a âŸ¨h, hasâŸ©
  Â· -- in this case, f is constant because all values are at top
    obtain âŸ¨aâ‚€, haâ‚€sâŸ© := hs
    exact âŸ¨aâ‚€, haâ‚€s, fun a ha => all_top a ha (f aâ‚€)âŸ©

theorem Filter.Tendsto.exists_forall_le [Nonempty Î±] [LinearOrder Î²] {f : Î± â†’ Î²}
    (hf : Tendsto f cofinite atTop) : âˆƒ aâ‚€, âˆ€ a, f aâ‚€ â‰¤ f a :=
  let âŸ¨aâ‚€, _, haâ‚€âŸ© := hf.exists_within_forall_le univ_nonempty
  âŸ¨aâ‚€, fun a => haâ‚€ a (mem_univ _)âŸ©

theorem Filter.Tendsto.exists_within_forall_ge [LinearOrder Î²] {s : Set Î±} (hs : s.Nonempty)
    {f : Î± â†’ Î²} (hf : Filter.Tendsto f Filter.cofinite Filter.atBot) :
    âˆƒ aâ‚€ âˆˆ s, âˆ€ a âˆˆ s, f a â‰¤ f aâ‚€ :=
  @Filter.Tendsto.exists_within_forall_le _ Î²áµ’áµˆ _ _ hs _ hf

theorem Filter.Tendsto.exists_forall_ge [Nonempty Î±] [LinearOrder Î²] {f : Î± â†’ Î²}
    (hf : Tendsto f cofinite atBot) : âˆƒ aâ‚€, âˆ€ a, f a â‰¤ f aâ‚€ :=
  @Filter.Tendsto.exists_forall_le _ Î²áµ’áµˆ _ _ _ hf

theorem Function.Surjective.le_map_cofinite {f : Î± â†’ Î²} (hf : Surjective f) :
    cofinite â‰¤ map f cofinite := fun _ h => .of_preimage h hf

/-- For an injective function `f`, inverse images of finite sets are finite. See also
`Filter.comap_cofinite_le` and `Function.Injective.comap_cofinite_eq`. -/
theorem Function.Injective.tendsto_cofinite {f : Î± â†’ Î²} (hf : Injective f) :
    Tendsto f cofinite cofinite := fun _ h => h.preimage hf.injOn

/-- For a function with finite fibres, inverse images of finite sets are finite. -/
theorem Filter.Tendsto.cofinite_of_finite_preimage_singleton {f : Î± â†’ Î²}
    (hf : âˆ€ b, Finite (f â»Â¹' {b})) : Tendsto f cofinite cofinite :=
  fun _ h => h.preimage' fun b _ â†¦ hf b

/-- The pullback of the `Filter.cofinite` under an injective function is equal to `Filter.cofinite`.
See also `Filter.comap_cofinite_le` and `Function.Injective.tendsto_cofinite`. -/
theorem Function.Injective.comap_cofinite_eq {f : Î± â†’ Î²} (hf : Injective f) :
    comap f cofinite = cofinite :=
  (comap_cofinite_le f).antisymm hf.tendsto_cofinite.le_comap

/-- An injective sequence `f : â„• â†’ â„•` tends to infinity at infinity. -/
theorem Function.Injective.nat_tendsto_atTop {f : â„• â†’ â„•} (hf : Injective f) :
    Tendsto f atTop atTop :=
  Nat.cofinite_eq_atTop â–¸ hf.tendsto_cofinite

lemma Function.update_eventuallyEq [DecidableEq Î±] (f : Î± â†’ Î²) (a : Î±) (b : Î²) :
    Function.update f a b =á¶ [ğ“Ÿ {a}á¶œ] f := by
  filter_upwards [mem_principal_self _] with u hu using Function.update_of_ne hu _ _

lemma Function.update_eventuallyEq_cofinite [DecidableEq Î±] (f : Î± â†’ Î²) (a : Î±) (b : Î²) :
    Function.update f a b =á¶ [cofinite] f :=
  (Function.update_eventuallyEq f a b).filter_mono (by simp)

/--
A filter is free iff it is smaller than the cofinite filter.
-/
theorem le_cofinite_iff_ker (f : Filter Î±) : f â‰¤ cofinite â†” f.ker = âˆ… := by
  rw [le_cofinite_iff_compl_singleton_mem, ker_def, iInterâ‚‚_eq_empty_iff]
  exact forall_congr' fun x => âŸ¨fun h => âŸ¨{x}á¶œ, h, by simpâŸ©,
    fun âŸ¨s, hs, hxâŸ© => mem_of_superset hs (by simpa using hx)âŸ©

lemma eq_principal_ker_sup_inf_principal_ker_compl (f : Filter Î±) :
    f = ğ“Ÿ f.ker âŠ” (f âŠ“ ğ“Ÿ f.kerá¶œ) := by
  rw [sup_inf_left]
  simpa using gi_principal_ker.gc.l_u_le f

lemma inf_principal_ker_compl_le_cofinite (f : Filter Î±) : f âŠ“ ğ“Ÿ f.kerá¶œ â‰¤ cofinite := by
  rw [le_cofinite_iff_ker, ker_inf, ker_principal, inter_compl_self]

/--
Every filter is the disjoint supremum of a principal filter and a free filter in a unique way.
-/
theorem existsUnique_eq_principal_sup_free (f : Filter Î±) :
    âˆƒ! p : Set Î± Ã— Filter Î±, p.2 â‰¤ cofinite âˆ§ Disjoint (ğ“Ÿ p.1) p.2 âˆ§ f = ğ“Ÿ p.1 âŠ” p.2 := by
  refine âŸ¨(f.ker, f âŠ“ ğ“Ÿ f.kerá¶œ), âŸ¨?_, ?_, ?_âŸ©, fun q hq => ?_âŸ©
  Â· exact inf_principal_ker_compl_le_cofinite f
  Â· rw [disjoint_principal_left]
    exact mem_inf_of_right (mem_principal_self f.kerá¶œ)
  Â· exact eq_principal_ker_sup_inf_principal_ker_compl f
  Â· have hqk := congrArg Filter.ker hq.2.2
    rw [ker_sup, ker_principal, (le_cofinite_iff_ker q.2).mp hq.1, union_empty] at hqk
    refine congrArgâ‚‚ Prod.mk hqk.symm (le_antisymm (le_inf ?_ ?_) ?_)
    Â· rw [hq.2.2]
      exact le_sup_right
    Â· rw [le_principal_iff, hqk, â† disjoint_principal_left]
      exact hq.2.1
    Â· rw [hqk, hq.2.2, inf_sup_right, inf_principal, inter_compl_self, principal_empty, bot_sup_eq]
      exact inf_le_left

/--
Every filter is the disjoint supremum of a principal filter and a free filter.
-/
theorem exists_eq_principal_sup_free (f : Filter Î±) :
    âˆƒ s g, g â‰¤ cofinite âˆ§ Disjoint (ğ“Ÿ s) g âˆ§ f = ğ“Ÿ s âŠ” g :=
  Prod.exists.mp (existsUnique_eq_principal_sup_free f).exists
