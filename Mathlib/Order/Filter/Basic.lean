/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Jeremy Avigad
-/
import Mathlib.Data.Set.Finite

/-!
# Theory of filters on sets

## Main definitions

* `Filter` : filters on a set;
* `Filter.principal` : filter of all sets containing a given set;
* `Filter.map`, `Filter.comap` : operations on filters;
* `Filter.Tendsto` : limit with respect to filters;
* `Filter.Eventually` : `f.eventually p` means `{x | p x} âˆˆ f`;
* `Filter.Frequently` : `f.frequently p` means `{x | Â¬p x} âˆ‰ f`;
* `filter_upwards [hâ‚, ..., hâ‚™]` :
  a tactic that takes a list of proofs `háµ¢ : sáµ¢ âˆˆ f`,
  and replaces a goal `s âˆˆ f` with `âˆ€ x, x âˆˆ sâ‚ â†’ ... â†’ x âˆˆ sâ‚™ â†’ x âˆˆ s`;
* `Filter.NeBot f` : a utility class stating that `f` is a non-trivial filter.

Filters on a type `X` are sets of sets of `X` satisfying three conditions. They are mostly used to
abstract two related kinds of ideas:
* *limits*, including finite or infinite limits of sequences, finite or infinite limits of functions
  at a point or at infinity, etc...
* *things happening eventually*, including things happening for large enough `n : â„•`, or near enough
  a point `x`, or for close enough pairs of points, or things happening almost everywhere in the
  sense of measure theory. Dually, filters can also express the idea of *things happening often*:
  for arbitrarily large `n`, or at a point in any neighborhood of given a point etc...

In this file, we define the type `Filter X` of filters on `X`, and endow it with a complete lattice
structure. This structure is lifted from the lattice structure on `Set (Set X)` using the Galois
insertion which maps a filter to its elements in one direction, and an arbitrary set of sets to
the smallest filter containing it in the other direction.
We also prove `Filter` is a monadic functor, with a push-forward operation
`Filter.map` and a pull-back operation `Filter.comap` that form a Galois connections for the
order on filters.

The examples of filters appearing in the description of the two motivating ideas are:
* `(Filter.atTop : Filter â„•)` : made of sets of `â„•` containing `{n | n â‰¥ N}` for some `N`
* `ğ“ x` : made of neighborhoods of `x` in a topological space (defined in topology.basic)
* `ğ“¤ X` : made of entourages of a uniform space (those space are generalizations of metric spaces
  defined in `Mathlib/Topology/UniformSpace/Basic.lean`)
* `MeasureTheory.ae` : made of sets whose complement has zero measure with respect to `Î¼`
  (defined in `Mathlib/MeasureTheory/OuterMeasure/AE`)

The general notion of limit of a map with respect to filters on the source and target types
is `Filter.Tendsto`. It is defined in terms of the order and the push-forward operation.
The predicate "happening eventually" is `Filter.Eventually`, and "happening often" is
`Filter.Frequently`, whose definitions are immediate after `Filter` is defined (but they come
rather late in this file in order to immediately relate them to the lattice structure).

For instance, anticipating on Topology.Basic, the statement: "if a sequence `u` converges to
some `x` and `u n` belongs to a set `M` for `n` large enough then `x` is in the closure of
`M`" is formalized as: `Tendsto u atTop (ğ“ x) â†’ (âˆ€á¶  n in atTop, u n âˆˆ M) â†’ x âˆˆ closure M`,
which is a special case of `mem_closure_of_tendsto` from Topology.Basic.

## Notations

* `âˆ€á¶  x in f, p x` : `f.Eventually p`;
* `âˆƒá¶  x in f, p x` : `f.Frequently p`;
* `f =á¶ [l] g` : `âˆ€á¶  x in l, f x = g x`;
* `f â‰¤á¶ [l] g` : `âˆ€á¶  x in l, f x â‰¤ g x`;
* `ğ“Ÿ s` : `Filter.Principal s`, localized in `Filter`.

## References

*  [N. Bourbaki, *General Topology*][bourbaki1966]

Important note: Bourbaki requires that a filter on `X` cannot contain all sets of `X`, which
we do *not* require. This gives `Filter X` better formal properties, in particular a bottom element
`âŠ¥` for its lattice structure, at the cost of including the assumption
`[NeBot f]` in a number of lemmas and definitions.
-/

assert_not_exists OrderedSemiring

open Function Set Order
open scoped symmDiff

universe u v w x y

/-- A filter `F` on a type `Î±` is a collection of sets of `Î±` which contains the whole `Î±`,
is upwards-closed, and is stable under intersection. We do not forbid this collection to be
all sets of `Î±`. -/
structure Filter (Î± : Type*) where
  /-- The set of sets that belong to the filter. -/
  sets : Set (Set Î±)
  /-- The set `Set.univ` belongs to any filter. -/
  univ_sets : Set.univ âˆˆ sets
  /-- If a set belongs to a filter, then its superset belongs to the filter as well. -/
  sets_of_superset {x y} : x âˆˆ sets â†’ x âŠ† y â†’ y âˆˆ sets
  /-- If two sets belong to a filter, then their intersection belongs to the filter as well. -/
  inter_sets {x y} : x âˆˆ sets â†’ y âˆˆ sets â†’ x âˆ© y âˆˆ sets

/-- If `F` is a filter on `Î±`, and `U` a subset of `Î±` then we can write `U âˆˆ F` as on paper. -/
instance {Î± : Type*} : Membership (Set Î±) (Filter Î±) :=
  âŸ¨fun F U => U âˆˆ F.setsâŸ©

namespace Filter

variable {Î± : Type u} {f g : Filter Î±} {s t : Set Î±}

@[simp]
protected theorem mem_mk {t : Set (Set Î±)} {hâ‚ hâ‚‚ hâ‚ƒ} : s âˆˆ mk t hâ‚ hâ‚‚ hâ‚ƒ â†” s âˆˆ t :=
  Iff.rfl

@[simp]
protected theorem mem_sets : s âˆˆ f.sets â†” s âˆˆ f :=
  Iff.rfl

instance inhabitedMem : Inhabited { s : Set Î± // s âˆˆ f } :=
  âŸ¨âŸ¨univ, f.univ_setsâŸ©âŸ©

theorem filter_eq : âˆ€ {f g : Filter Î±}, f.sets = g.sets â†’ f = g
  | âŸ¨_, _, _, _âŸ©, âŸ¨_, _, _, _âŸ©, rfl => rfl

theorem filter_eq_iff : f = g â†” f.sets = g.sets :=
  âŸ¨congr_arg _, filter_eqâŸ©

@[ext]
protected theorem ext (h : âˆ€ s, s âˆˆ f â†” s âˆˆ g) : f = g := by
  simpa [filter_eq_iff, Set.ext_iff, Filter.mem_sets]

/-- An extensionality lemma that is useful for filters with good lemmas about `sá¶œ âˆˆ f` (e.g.,
`Filter.comap`, `Filter.coprod`, `Filter.Coprod`, `Filter.cofinite`). -/
protected theorem coext (h : âˆ€ s, sá¶œ âˆˆ f â†” sá¶œ âˆˆ g) : f = g :=
  Filter.ext <| compl_surjective.forall.2 h

@[simp]
theorem univ_mem : univ âˆˆ f :=
  f.univ_sets

theorem mem_of_superset {x y : Set Î±} (hx : x âˆˆ f) (hxy : x âŠ† y) : y âˆˆ f :=
  f.sets_of_superset hx hxy

instance : Trans (Â· âŠ‡ Â·) ((Â· âˆˆ Â·) : Set Î± â†’ Filter Î± â†’ Prop) (Â· âˆˆ Â·) where
  trans hâ‚ hâ‚‚ := mem_of_superset hâ‚‚ hâ‚

instance : Trans Membership.mem (Â· âŠ† Â·) (Membership.mem : Filter Î± â†’ Set Î± â†’ Prop) where
  trans hâ‚ hâ‚‚ := mem_of_superset hâ‚ hâ‚‚

theorem inter_mem {s t : Set Î±} (hs : s âˆˆ f) (ht : t âˆˆ f) : s âˆ© t âˆˆ f :=
  f.inter_sets hs ht

@[simp]
theorem inter_mem_iff {s t : Set Î±} : s âˆ© t âˆˆ f â†” s âˆˆ f âˆ§ t âˆˆ f :=
  âŸ¨fun h => âŸ¨mem_of_superset h inter_subset_left, mem_of_superset h inter_subset_rightâŸ©,
    and_imp.2 inter_memâŸ©

theorem diff_mem {s t : Set Î±} (hs : s âˆˆ f) (ht : tá¶œ âˆˆ f) : s \ t âˆˆ f :=
  inter_mem hs ht

theorem univ_mem' (h : âˆ€ a, a âˆˆ s) : s âˆˆ f :=
  mem_of_superset univ_mem fun x _ => h x

theorem mp_mem (hs : s âˆˆ f) (h : { x | x âˆˆ s â†’ x âˆˆ t } âˆˆ f) : t âˆˆ f :=
  mem_of_superset (inter_mem hs h) fun _ âŸ¨hâ‚, hâ‚‚âŸ© => hâ‚‚ hâ‚

theorem congr_sets (h : { x | x âˆˆ s â†” x âˆˆ t } âˆˆ f) : s âˆˆ f â†” t âˆˆ f :=
  âŸ¨fun hs => mp_mem hs (mem_of_superset h fun _ => Iff.mp), fun hs =>
    mp_mem hs (mem_of_superset h fun _ => Iff.mpr)âŸ©

/-- Override `sets` field of a filter to provide better definitional equality. -/
protected def copy (f : Filter Î±) (S : Set (Set Î±)) (hmem : âˆ€ s, s âˆˆ S â†” s âˆˆ f) : Filter Î± where
  sets := S
  univ_sets := (hmem _).2 univ_mem
  sets_of_superset h hsub := (hmem _).2 <| mem_of_superset ((hmem _).1 h) hsub
  inter_sets hâ‚ hâ‚‚ := (hmem _).2 <| inter_mem ((hmem _).1 hâ‚) ((hmem _).1 hâ‚‚)

lemma copy_eq {S} (hmem : âˆ€ s, s âˆˆ S â†” s âˆˆ f) : f.copy S hmem = f := Filter.ext hmem

@[simp] lemma mem_copy {S hmem} : s âˆˆ f.copy S hmem â†” s âˆˆ S := Iff.rfl

@[simp]
theorem biInter_mem {Î² : Type v} {s : Î² â†’ Set Î±} {is : Set Î²} (hf : is.Finite) :
    (â‹‚ i âˆˆ is, s i) âˆˆ f â†” âˆ€ i âˆˆ is, s i âˆˆ f :=
  Finite.induction_on hf (by simp) fun _ _ hs => by simp [hs]

@[simp]
theorem biInter_finset_mem {Î² : Type v} {s : Î² â†’ Set Î±} (is : Finset Î²) :
    (â‹‚ i âˆˆ is, s i) âˆˆ f â†” âˆ€ i âˆˆ is, s i âˆˆ f :=
  biInter_mem is.finite_toSet

alias _root_.Finset.iInter_mem_sets := biInter_finset_mem

-- attribute [protected] Finset.iInter_mem_sets porting note: doesn't work

@[simp]
theorem sInter_mem {s : Set (Set Î±)} (hfin : s.Finite) : â‹‚â‚€ s âˆˆ f â†” âˆ€ U âˆˆ s, U âˆˆ f := by
  rw [sInter_eq_biInter, biInter_mem hfin]

@[simp]
theorem iInter_mem {Î² : Sort v} {s : Î² â†’ Set Î±} [Finite Î²] : (â‹‚ i, s i) âˆˆ f â†” âˆ€ i, s i âˆˆ f :=
  (sInter_mem (finite_range _)).trans forall_mem_range

theorem exists_mem_subset_iff : (âˆƒ t âˆˆ f, t âŠ† s) â†” s âˆˆ f :=
  âŸ¨fun âŸ¨_, ht, tsâŸ© => mem_of_superset ht ts, fun hs => âŸ¨s, hs, Subset.rflâŸ©âŸ©

theorem monotone_mem {f : Filter Î±} : Monotone fun s => s âˆˆ f := fun _ _ hst h =>
  mem_of_superset h hst

theorem exists_mem_and_iff {P : Set Î± â†’ Prop} {Q : Set Î± â†’ Prop} (hP : Antitone P)
    (hQ : Antitone Q) : ((âˆƒ u âˆˆ f, P u) âˆ§ âˆƒ u âˆˆ f, Q u) â†” âˆƒ u âˆˆ f, P u âˆ§ Q u := by
  constructor
  Â· rintro âŸ¨âŸ¨u, huf, hPuâŸ©, v, hvf, hQvâŸ©
    exact
      âŸ¨u âˆ© v, inter_mem huf hvf, hP inter_subset_left hPu, hQ inter_subset_right hQvâŸ©
  Â· rintro âŸ¨u, huf, hPu, hQuâŸ©
    exact âŸ¨âŸ¨u, huf, hPuâŸ©, u, huf, hQuâŸ©

theorem forall_in_swap {Î² : Type*} {p : Set Î± â†’ Î² â†’ Prop} :
    (âˆ€ a âˆˆ f, âˆ€ (b), p a b) â†” âˆ€ (b), âˆ€ a âˆˆ f, p a b :=
  Set.forall_in_swap

end Filter

namespace Mathlib.Tactic

open Lean Meta Elab Tactic

/--
`filter_upwards [hâ‚, â‹¯, hâ‚™]` replaces a goal of the form `s âˆˆ f` and terms
`hâ‚ : tâ‚ âˆˆ f, â‹¯, hâ‚™ : tâ‚™ âˆˆ f` with `âˆ€ x, x âˆˆ tâ‚ â†’ â‹¯ â†’ x âˆˆ tâ‚™ â†’ x âˆˆ s`.
The list is an optional parameter, `[]` being its default value.

`filter_upwards [hâ‚, â‹¯, hâ‚™] with aâ‚ aâ‚‚ â‹¯ aâ‚–` is a short form for
`{ filter_upwards [hâ‚, â‹¯, hâ‚™], intros aâ‚ aâ‚‚ â‹¯ aâ‚– }`.

`filter_upwards [hâ‚, â‹¯, hâ‚™] using e` is a short form for
`{ filter_upwards [h1, â‹¯, hn], exact e }`.

Combining both shortcuts is done by writing `filter_upwards [hâ‚, â‹¯, hâ‚™] with aâ‚ aâ‚‚ â‹¯ aâ‚– using e`.
Note that in this case, the `aáµ¢` terms can be used in `e`.
-/
syntax (name := filterUpwards) "filter_upwards" (" [" term,* "]")?
  (" with" (ppSpace colGt term:max)*)? (" using " term)? : tactic

elab_rules : tactic
| `(tactic| filter_upwards $[[$[$args],*]]? $[with $wth*]? $[using $usingArg]?) => do
  let config : ApplyConfig := {newGoals := ApplyNewGoals.nonDependentOnly}
  for e in args.getD #[] |>.reverse do
    let goal â† getMainGoal
    replaceMainGoal <| â† goal.withContext <| runTermElab do
      let m â† mkFreshExprMVar none
      let lem â† Term.elabTermEnsuringType
        (â† ``(Filter.mp_mem $e $(â† Term.exprToSyntax m))) (â† goal.getType)
      goal.assign lem
      return [m.mvarId!]
  liftMetaTactic fun goal => do
    goal.apply (â† mkConstWithFreshMVarLevels ``Filter.univ_mem') config
  evalTactic <|â† `(tactic| dsimp (config := {zeta := false}) only [Set.mem_setOf_eq])
  if let some l := wth then
    evalTactic <|â† `(tactic| intro $[$l]*)
  if let some e := usingArg then
    evalTactic <|â† `(tactic| exact $e)

end Mathlib.Tactic

namespace Filter

variable {Î± : Type u} {Î² : Type v} {Î³ : Type w} {Î´ : Type*} {Î¹ : Sort x}

section Principal

/-- The principal filter of `s` is the collection of all supersets of `s`. -/
def principal (s : Set Î±) : Filter Î± where
  sets := { t | s âŠ† t }
  univ_sets := subset_univ s
  sets_of_superset hx := Subset.trans hx
  inter_sets := subset_inter

@[inherit_doc]
scoped notation "ğ“Ÿ" => Filter.principal

@[simp] theorem mem_principal {s t : Set Î±} : s âˆˆ ğ“Ÿ t â†” t âŠ† s := Iff.rfl

theorem mem_principal_self (s : Set Î±) : s âˆˆ ğ“Ÿ s := Subset.rfl

end Principal

open Filter

section Join

/-- The join of a filter of filters is defined by the relation `s âˆˆ join f â†” {t | s âˆˆ t} âˆˆ f`. -/
def join (f : Filter (Filter Î±)) : Filter Î± where
  sets := { s | { t : Filter Î± | s âˆˆ t } âˆˆ f }
  univ_sets := by simp only [mem_setOf_eq, univ_sets, â† Filter.mem_sets, setOf_true]
  sets_of_superset hx xy := mem_of_superset hx fun f h => mem_of_superset h xy
  inter_sets hx hy := mem_of_superset (inter_mem hx hy) fun f âŸ¨hâ‚, hâ‚‚âŸ© => inter_mem hâ‚ hâ‚‚

@[simp]
theorem mem_join {s : Set Î±} {f : Filter (Filter Î±)} : s âˆˆ join f â†” { t | s âˆˆ t } âˆˆ f :=
  Iff.rfl

end Join

section Lattice

variable {f g : Filter Î±} {s t : Set Î±}

instance : PartialOrder (Filter Î±) where
  le f g := âˆ€ â¦ƒU : Set Î±â¦„, U âˆˆ g â†’ U âˆˆ f
  le_antisymm a b hâ‚ hâ‚‚ := filter_eq <| Subset.antisymm hâ‚‚ hâ‚
  le_refl a := Subset.rfl
  le_trans a b c hâ‚ hâ‚‚ := Subset.trans hâ‚‚ hâ‚

theorem le_def : f â‰¤ g â†” âˆ€ x âˆˆ g, x âˆˆ f :=
  Iff.rfl

protected theorem not_le : Â¬f â‰¤ g â†” âˆƒ s âˆˆ g, s âˆ‰ f := by simp_rw [le_def, not_forall, exists_prop]

/-- `GenerateSets g s`: `s` is in the filter closure of `g`. -/
inductive GenerateSets (g : Set (Set Î±)) : Set Î± â†’ Prop
  | basic {s : Set Î±} : s âˆˆ g â†’ GenerateSets g s
  | univ : GenerateSets g univ
  | superset {s t : Set Î±} : GenerateSets g s â†’ s âŠ† t â†’ GenerateSets g t
  | inter {s t : Set Î±} : GenerateSets g s â†’ GenerateSets g t â†’ GenerateSets g (s âˆ© t)

/-- `generate g` is the largest filter containing the sets `g`. -/
def generate (g : Set (Set Î±)) : Filter Î± where
  sets := {s | GenerateSets g s}
  univ_sets := GenerateSets.univ
  sets_of_superset := GenerateSets.superset
  inter_sets := GenerateSets.inter

lemma mem_generate_of_mem {s : Set <| Set Î±} {U : Set Î±} (h : U âˆˆ s) :
    U âˆˆ generate s := GenerateSets.basic h

theorem le_generate_iff {s : Set (Set Î±)} {f : Filter Î±} : f â‰¤ generate s â†” s âŠ† f.sets :=
  Iff.intro (fun h _ hu => h <| GenerateSets.basic <| hu) fun h _ hu =>
    hu.recOn (fun h' => h h') univ_mem (fun _ hxy hx => mem_of_superset hx hxy) fun _ _ hx hy =>
      inter_mem hx hy

theorem mem_generate_iff {s : Set <| Set Î±} {U : Set Î±} :
    U âˆˆ generate s â†” âˆƒ t âŠ† s, Set.Finite t âˆ§ â‹‚â‚€ t âŠ† U := by
  constructor <;> intro h
  Â· induction h with
    | @basic V V_in =>
      exact âŸ¨{V}, singleton_subset_iff.2 V_in, finite_singleton _, (sInter_singleton _).subsetâŸ©
    | univ => exact âŸ¨âˆ…, empty_subset _, finite_empty, subset_univ _âŸ©
    | superset _ hVW hV =>
      rcases hV with âŸ¨t, hts, ht, htVâŸ©
      exact âŸ¨t, hts, ht, htV.trans hVWâŸ©
    | inter _ _ hV hW =>
      rcases hV, hW with âŸ¨âŸ¨t, hts, ht, htVâŸ©, u, hus, hu, huWâŸ©
      exact
        âŸ¨t âˆª u, union_subset hts hus, ht.union hu,
          (sInter_union _ _).subset.trans <| inter_subset_inter htV huWâŸ©
  Â· rcases h with âŸ¨t, hts, tfin, hâŸ©
    exact mem_of_superset ((sInter_mem tfin).2 fun V hV => GenerateSets.basic <| hts hV) h

@[simp] lemma generate_singleton (s : Set Î±) : generate {s} = ğ“Ÿ s :=
  le_antisymm (fun _t ht â†¦ mem_of_superset (mem_generate_of_mem <| mem_singleton _) ht) <|
    le_generate_iff.2 <| singleton_subset_iff.2 Subset.rfl

/-- `mkOfClosure s hs` constructs a filter on `Î±` whose elements set is exactly
`s : Set (Set Î±)`, provided one gives the assumption `hs : (generate s).sets = s`. -/
protected def mkOfClosure (s : Set (Set Î±)) (hs : (generate s).sets = s) : Filter Î± where
  sets := s
  univ_sets := hs â–¸ univ_mem
  sets_of_superset := hs â–¸ mem_of_superset
  inter_sets := hs â–¸ inter_mem

theorem mkOfClosure_sets {s : Set (Set Î±)} {hs : (generate s).sets = s} :
    Filter.mkOfClosure s hs = generate s :=
  Filter.ext fun u =>
    show u âˆˆ (Filter.mkOfClosure s hs).sets â†” u âˆˆ (generate s).sets from hs.symm â–¸ Iff.rfl

/-- Galois insertion from sets of sets into filters. -/
def giGenerate (Î± : Type*) :
    @GaloisInsertion (Set (Set Î±)) (Filter Î±)áµ’áµˆ _ _ Filter.generate Filter.sets where
  gc _ _ := le_generate_iff
  le_l_u _ _ h := GenerateSets.basic h
  choice s hs := Filter.mkOfClosure s (le_antisymm hs <| le_generate_iff.1 <| le_rfl)
  choice_eq _ _ := mkOfClosure_sets

/-- The infimum of filters is the filter generated by intersections
  of elements of the two filters. -/
instance : Inf (Filter Î±) :=
  âŸ¨fun f g : Filter Î± =>
    { sets := { s | âˆƒ a âˆˆ f, âˆƒ b âˆˆ g, s = a âˆ© b }
      univ_sets := âŸ¨_, univ_mem, _, univ_mem, by simpâŸ©
      sets_of_superset := by
        rintro x y âŸ¨a, ha, b, hb, rflâŸ© xy
        refine
          âŸ¨a âˆª y, mem_of_superset ha subset_union_left, b âˆª y,
            mem_of_superset hb subset_union_left, ?_âŸ©
        rw [â† inter_union_distrib_right, union_eq_self_of_subset_left xy]
      inter_sets := by
        rintro x y âŸ¨a, ha, b, hb, rflâŸ© âŸ¨c, hc, d, hd, rflâŸ©
        refine âŸ¨a âˆ© c, inter_mem ha hc, b âˆ© d, inter_mem hb hd, ?_âŸ©
        ac_rfl }âŸ©

theorem mem_inf_iff {f g : Filter Î±} {s : Set Î±} : s âˆˆ f âŠ“ g â†” âˆƒ tâ‚ âˆˆ f, âˆƒ tâ‚‚ âˆˆ g, s = tâ‚ âˆ© tâ‚‚ :=
  Iff.rfl

theorem mem_inf_of_left {f g : Filter Î±} {s : Set Î±} (h : s âˆˆ f) : s âˆˆ f âŠ“ g :=
  âŸ¨s, h, univ, univ_mem, (inter_univ s).symmâŸ©

theorem mem_inf_of_right {f g : Filter Î±} {s : Set Î±} (h : s âˆˆ g) : s âˆˆ f âŠ“ g :=
  âŸ¨univ, univ_mem, s, h, (univ_inter s).symmâŸ©

theorem inter_mem_inf {Î± : Type u} {f g : Filter Î±} {s t : Set Î±} (hs : s âˆˆ f) (ht : t âˆˆ g) :
    s âˆ© t âˆˆ f âŠ“ g :=
  âŸ¨s, hs, t, ht, rflâŸ©

theorem mem_inf_of_inter {f g : Filter Î±} {s t u : Set Î±} (hs : s âˆˆ f) (ht : t âˆˆ g)
    (h : s âˆ© t âŠ† u) : u âˆˆ f âŠ“ g :=
  mem_of_superset (inter_mem_inf hs ht) h

theorem mem_inf_iff_superset {f g : Filter Î±} {s : Set Î±} :
    s âˆˆ f âŠ“ g â†” âˆƒ tâ‚ âˆˆ f, âˆƒ tâ‚‚ âˆˆ g, tâ‚ âˆ© tâ‚‚ âŠ† s :=
  âŸ¨fun âŸ¨tâ‚, hâ‚, tâ‚‚, hâ‚‚, EqâŸ© => âŸ¨tâ‚, hâ‚, tâ‚‚, hâ‚‚, Eq â–¸ Subset.rflâŸ©, fun âŸ¨_, hâ‚, _, hâ‚‚, subâŸ© =>
    mem_inf_of_inter hâ‚ hâ‚‚ subâŸ©

instance : Top (Filter Î±) :=
  âŸ¨{  sets := { s | âˆ€ x, x âˆˆ s }
      univ_sets := fun x => mem_univ x
      sets_of_superset := fun hx hxy a => hxy (hx a)
      inter_sets := fun hx hy _ => mem_inter (hx _) (hy _) }âŸ©

theorem mem_top_iff_forall {s : Set Î±} : s âˆˆ (âŠ¤ : Filter Î±) â†” âˆ€ x, x âˆˆ s :=
  Iff.rfl

@[simp]
theorem mem_top {s : Set Î±} : s âˆˆ (âŠ¤ : Filter Î±) â†” s = univ := by
  rw [mem_top_iff_forall, eq_univ_iff_forall]

section CompleteLattice

/- We lift the complete lattice along the Galois connection `generate` / `sets`. Unfortunately,
  we want to have different definitional equalities for some lattice operations. So we define them
  upfront and change the lattice operations for the complete lattice instance. -/
instance instCompleteLatticeFilter : CompleteLattice (Filter Î±) :=
  { @OrderDual.instCompleteLattice _ (giGenerate Î±).liftCompleteLattice with
    le := (Â· â‰¤ Â·)
    top := âŠ¤
    le_top := fun _ _s hs => (mem_top.1 hs).symm â–¸ univ_mem
    inf := (Â· âŠ“ Â·)
    inf_le_left := fun _ _ _ => mem_inf_of_left
    inf_le_right := fun _ _ _ => mem_inf_of_right
    le_inf := fun _ _ _ hâ‚ hâ‚‚ _s âŸ¨_a, ha, _b, hb, hsâŸ© => hs.symm â–¸ inter_mem (hâ‚ ha) (hâ‚‚ hb)
    sSup := join âˆ˜ ğ“Ÿ
    le_sSup := fun _ _f hf _s hs => hs hf
    sSup_le := fun _ _f hf _s hs _g hg => hf _ hg hs }

instance : Inhabited (Filter Î±) := âŸ¨âŠ¥âŸ©

end CompleteLattice

/-- A filter is `NeBot` if it is not equal to `âŠ¥`, or equivalently the empty set does not belong to
the filter. Bourbaki include this assumption in the definition of a filter but we prefer to have a
`CompleteLattice` structure on `Filter _`, so we use a typeclass argument in lemmas instead. -/
class NeBot (f : Filter Î±) : Prop where
  /-- The filter is nontrivial: `f â‰  âŠ¥` or equivalently, `âˆ… âˆ‰ f`. -/
  ne' : f â‰  âŠ¥

theorem neBot_iff {f : Filter Î±} : NeBot f â†” f â‰  âŠ¥ :=
  âŸ¨fun h => h.1, fun h => âŸ¨hâŸ©âŸ©

theorem NeBot.ne {f : Filter Î±} (hf : NeBot f) : f â‰  âŠ¥ := hf.ne'

@[simp] theorem not_neBot {f : Filter Î±} : Â¬f.NeBot â†” f = âŠ¥ := neBot_iff.not_left

theorem NeBot.mono {f g : Filter Î±} (hf : NeBot f) (hg : f â‰¤ g) : NeBot g :=
  âŸ¨ne_bot_of_le_ne_bot hf.1 hgâŸ©

theorem neBot_of_le {f g : Filter Î±} [hf : NeBot f] (hg : f â‰¤ g) : NeBot g :=
  hf.mono hg

@[simp] theorem sup_neBot {f g : Filter Î±} : NeBot (f âŠ” g) â†” NeBot f âˆ¨ NeBot g := by
  simp only [neBot_iff, not_and_or, Ne, sup_eq_bot_iff]

theorem not_disjoint_self_iff : Â¬Disjoint f f â†” f.NeBot := by rw [disjoint_self, neBot_iff]

theorem bot_sets_eq : (âŠ¥ : Filter Î±).sets = univ := rfl

/-- Either `f = âŠ¥` or `Filter.NeBot f`. This is a version of `eq_or_ne` that uses `Filter.NeBot`
as the second alternative, to be used as an instance. -/
theorem eq_or_neBot (f : Filter Î±) : f = âŠ¥ âˆ¨ NeBot f := (eq_or_ne f âŠ¥).imp_right NeBot.mk

theorem sup_sets_eq {f g : Filter Î±} : (f âŠ” g).sets = f.sets âˆ© g.sets :=
  (giGenerate Î±).gc.u_inf

theorem sSup_sets_eq {s : Set (Filter Î±)} : (sSup s).sets = â‹‚ f âˆˆ s, (f : Filter Î±).sets :=
  (giGenerate Î±).gc.u_sInf

theorem iSup_sets_eq {f : Î¹ â†’ Filter Î±} : (iSup f).sets = â‹‚ i, (f i).sets :=
  (giGenerate Î±).gc.u_iInf

theorem generate_empty : Filter.generate âˆ… = (âŠ¤ : Filter Î±) :=
  (giGenerate Î±).gc.l_bot

theorem generate_univ : Filter.generate univ = (âŠ¥ : Filter Î±) :=
  bot_unique fun _ _ => GenerateSets.basic (mem_univ _)

theorem generate_union {s t : Set (Set Î±)} :
    Filter.generate (s âˆª t) = Filter.generate s âŠ“ Filter.generate t :=
  (giGenerate Î±).gc.l_sup

theorem generate_iUnion {s : Î¹ â†’ Set (Set Î±)} :
    Filter.generate (â‹ƒ i, s i) = â¨… i, Filter.generate (s i) :=
  (giGenerate Î±).gc.l_iSup

@[simp]
theorem mem_bot {s : Set Î±} : s âˆˆ (âŠ¥ : Filter Î±) :=
  trivial

@[simp]
theorem mem_sup {f g : Filter Î±} {s : Set Î±} : s âˆˆ f âŠ” g â†” s âˆˆ f âˆ§ s âˆˆ g :=
  Iff.rfl

theorem union_mem_sup {f g : Filter Î±} {s t : Set Î±} (hs : s âˆˆ f) (ht : t âˆˆ g) : s âˆª t âˆˆ f âŠ” g :=
  âŸ¨mem_of_superset hs subset_union_left, mem_of_superset ht subset_union_rightâŸ©

@[simp]
theorem mem_sSup {x : Set Î±} {s : Set (Filter Î±)} : x âˆˆ sSup s â†” âˆ€ f âˆˆ s, x âˆˆ (f : Filter Î±) :=
  Iff.rfl

@[simp]
theorem mem_iSup {x : Set Î±} {f : Î¹ â†’ Filter Î±} : x âˆˆ iSup f â†” âˆ€ i, x âˆˆ f i := by
  simp only [â† Filter.mem_sets, iSup_sets_eq, iff_self_iff, mem_iInter]

@[simp]
theorem iSup_neBot {f : Î¹ â†’ Filter Î±} : (â¨† i, f i).NeBot â†” âˆƒ i, (f i).NeBot := by
  simp [neBot_iff]

theorem iInf_eq_generate (s : Î¹ â†’ Filter Î±) : iInf s = generate (â‹ƒ i, (s i).sets) :=
  show generate _ = generate _ from congr_arg _ <| congr_arg sSup <| (range_comp _ _).symm

theorem mem_iInf_of_mem {f : Î¹ â†’ Filter Î±} (i : Î¹) {s} (hs : s âˆˆ f i) : s âˆˆ â¨… i, f i :=
  iInf_le f i hs

theorem mem_iInf_of_iInter {Î¹} {s : Î¹ â†’ Filter Î±} {U : Set Î±} {I : Set Î¹} (I_fin : I.Finite)
    {V : I â†’ Set Î±} (hV : âˆ€ (i : I), V i âˆˆ s i) (hU : â‹‚ i, V i âŠ† U) : U âˆˆ â¨… i, s i := by
  haveI := I_fin.fintype
  refine mem_of_superset (iInter_mem.2 fun i => ?_) hU
  exact mem_iInf_of_mem (i : Î¹) (hV _)

theorem mem_iInf {Î¹} {s : Î¹ â†’ Filter Î±} {U : Set Î±} :
    (U âˆˆ â¨… i, s i) â†”
      âˆƒ I : Set Î¹, I.Finite âˆ§ âˆƒ V : I â†’ Set Î±, (âˆ€ (i : I), V i âˆˆ s i) âˆ§ U = â‹‚ i, V i := by
  constructor
  Â· rw [iInf_eq_generate, mem_generate_iff]
    rintro âŸ¨t, tsub, tfin, tinterâŸ©
    rcases eq_finite_iUnion_of_finite_subset_iUnion tfin tsub with âŸ¨I, Ifin, Ïƒ, Ïƒfin, Ïƒsub, rflâŸ©
    rw [sInter_iUnion] at tinter
    set V := fun i => U âˆª â‹‚â‚€ Ïƒ i with hV
    have V_in : âˆ€ (i : I), V i âˆˆ s i := by
      rintro i
      have : â‹‚â‚€ Ïƒ i âˆˆ s i := by
        rw [sInter_mem (Ïƒfin _)]
        apply Ïƒsub
      exact mem_of_superset this subset_union_right
    refine âŸ¨I, Ifin, V, V_in, ?_âŸ©
    rwa [hV, â† union_iInter, union_eq_self_of_subset_right]
  Â· rintro âŸ¨I, Ifin, V, V_in, rflâŸ©
    exact mem_iInf_of_iInter Ifin V_in Subset.rfl

theorem mem_iInf' {Î¹} {s : Î¹ â†’ Filter Î±} {U : Set Î±} :
    (U âˆˆ â¨… i, s i) â†”
      âˆƒ I : Set Î¹, I.Finite âˆ§ âˆƒ V : Î¹ â†’ Set Î±, (âˆ€ i, V i âˆˆ s i) âˆ§
        (âˆ€ i âˆ‰ I, V i = univ) âˆ§ (U = â‹‚ i âˆˆ I, V i) âˆ§ U = â‹‚ i, V i := by
  classical
  simp only [mem_iInf, SetCoe.forall', biInter_eq_iInter]
  refine âŸ¨?_, fun âŸ¨I, If, V, hVs, _, hVU, _âŸ© => âŸ¨I, If, fun i => V i, fun i => hVs i, hVUâŸ©âŸ©
  rintro âŸ¨I, If, V, hV, rflâŸ©
  refine âŸ¨I, If, fun i => if hi : i âˆˆ I then V âŸ¨i, hiâŸ© else univ, fun i => ?_, fun i hi => ?_, ?_âŸ©
  Â· dsimp only
    split_ifs
    exacts [hV âŸ¨i,_âŸ©, univ_mem]
  Â· exact dif_neg hi
  Â· simp only [iInter_dite, biInter_eq_iInter, dif_pos (Subtype.coe_prop _), Subtype.coe_eta,
      iInter_univ, inter_univ, eq_self_iff_true, true_and_iff]

theorem exists_iInter_of_mem_iInf {Î¹ : Type*} {Î± : Type*} {f : Î¹ â†’ Filter Î±} {s}
    (hs : s âˆˆ â¨… i, f i) : âˆƒ t : Î¹ â†’ Set Î±, (âˆ€ i, t i âˆˆ f i) âˆ§ s = â‹‚ i, t i :=
  let âŸ¨_, _, V, hVs, _, _, hVU'âŸ© := mem_iInf'.1 hs; âŸ¨V, hVs, hVU'âŸ©

theorem mem_iInf_of_finite {Î¹ : Type*} [Finite Î¹] {Î± : Type*} {f : Î¹ â†’ Filter Î±} (s) :
    (s âˆˆ â¨… i, f i) â†” âˆƒ t : Î¹ â†’ Set Î±, (âˆ€ i, t i âˆˆ f i) âˆ§ s = â‹‚ i, t i := by
  refine âŸ¨exists_iInter_of_mem_iInf, ?_âŸ©
  rintro âŸ¨t, ht, rflâŸ©
  exact iInter_mem.2 fun i => mem_iInf_of_mem i (ht i)

@[simp]
theorem le_principal_iff {s : Set Î±} {f : Filter Î±} : f â‰¤ ğ“Ÿ s â†” s âˆˆ f :=
  âŸ¨fun h => h Subset.rfl, fun hs _ ht => mem_of_superset hs htâŸ©

theorem Iic_principal (s : Set Î±) : Iic (ğ“Ÿ s) = { l | s âˆˆ l } :=
  Set.ext fun _ => le_principal_iff

theorem principal_mono {s t : Set Î±} : ğ“Ÿ s â‰¤ ğ“Ÿ t â†” s âŠ† t := by
  simp only [le_principal_iff, iff_self_iff, mem_principal]

@[gcongr] alias âŸ¨_, _root_.GCongr.filter_principal_monoâŸ© := principal_mono

@[mono]
theorem monotone_principal : Monotone (ğ“Ÿ : Set Î± â†’ Filter Î±) := fun _ _ => principal_mono.2

@[simp] theorem principal_eq_iff_eq {s t : Set Î±} : ğ“Ÿ s = ğ“Ÿ t â†” s = t := by
  simp only [le_antisymm_iff, le_principal_iff, mem_principal]; rfl

@[simp] theorem join_principal_eq_sSup {s : Set (Filter Î±)} : join (ğ“Ÿ s) = sSup s := rfl

@[simp] theorem principal_univ : ğ“Ÿ (univ : Set Î±) = âŠ¤ :=
  top_unique <| by simp only [le_principal_iff, mem_top, eq_self_iff_true]

@[simp]
theorem principal_empty : ğ“Ÿ (âˆ… : Set Î±) = âŠ¥ :=
  bot_unique fun _ _ => empty_subset _

theorem generate_eq_biInf (S : Set (Set Î±)) : generate S = â¨… s âˆˆ S, ğ“Ÿ s :=
  eq_of_forall_le_iff fun f => by simp [le_generate_iff, le_principal_iff, subset_def]

/-! ### Lattice equations -/

theorem empty_mem_iff_bot {f : Filter Î±} : âˆ… âˆˆ f â†” f = âŠ¥ :=
  âŸ¨fun h => bot_unique fun s _ => mem_of_superset h (empty_subset s), fun h => h.symm â–¸ mem_botâŸ©

theorem nonempty_of_mem {f : Filter Î±} [hf : NeBot f] {s : Set Î±} (hs : s âˆˆ f) : s.Nonempty :=
  s.eq_empty_or_nonempty.elim (fun h => absurd hs (h.symm â–¸ mt empty_mem_iff_bot.mp hf.1)) id

theorem NeBot.nonempty_of_mem {f : Filter Î±} (hf : NeBot f) {s : Set Î±} (hs : s âˆˆ f) : s.Nonempty :=
  @Filter.nonempty_of_mem Î± f hf s hs

@[simp]
theorem empty_not_mem (f : Filter Î±) [NeBot f] : Â¬âˆ… âˆˆ f := fun h => (nonempty_of_mem h).ne_empty rfl

theorem nonempty_of_neBot (f : Filter Î±) [NeBot f] : Nonempty Î± :=
  nonempty_of_exists <| nonempty_of_mem (univ_mem : univ âˆˆ f)

theorem compl_not_mem {f : Filter Î±} {s : Set Î±} [NeBot f] (h : s âˆˆ f) : sá¶œ âˆ‰ f := fun hsc =>
  (nonempty_of_mem (inter_mem h hsc)).ne_empty <| inter_compl_self s

theorem filter_eq_bot_of_isEmpty [IsEmpty Î±] (f : Filter Î±) : f = âŠ¥ :=
  empty_mem_iff_bot.mp <| univ_mem' isEmptyElim

protected lemma disjoint_iff {f g : Filter Î±} : Disjoint f g â†” âˆƒ s âˆˆ f, âˆƒ t âˆˆ g, Disjoint s t := by
  simp only [disjoint_iff, â† empty_mem_iff_bot, mem_inf_iff, inf_eq_inter, bot_eq_empty,
    @eq_comm _ âˆ…]

theorem disjoint_of_disjoint_of_mem {f g : Filter Î±} {s t : Set Î±} (h : Disjoint s t) (hs : s âˆˆ f)
    (ht : t âˆˆ g) : Disjoint f g :=
  Filter.disjoint_iff.mpr âŸ¨s, hs, t, ht, hâŸ©

theorem NeBot.not_disjoint (hf : f.NeBot) (hs : s âˆˆ f) (ht : t âˆˆ f) : Â¬Disjoint s t := fun h =>
  not_disjoint_self_iff.2 hf <| Filter.disjoint_iff.2 âŸ¨s, hs, t, ht, hâŸ©

theorem inf_eq_bot_iff {f g : Filter Î±} : f âŠ“ g = âŠ¥ â†” âˆƒ U âˆˆ f, âˆƒ V âˆˆ g, U âˆ© V = âˆ… := by
  simp only [â† disjoint_iff, Filter.disjoint_iff, Set.disjoint_iff_inter_eq_empty]

theorem _root_.Pairwise.exists_mem_filter_of_disjoint {Î¹ : Type*} [Finite Î¹] {l : Î¹ â†’ Filter Î±}
    (hd : Pairwise (Disjoint on l)) :
    âˆƒ s : Î¹ â†’ Set Î±, (âˆ€ i, s i âˆˆ l i) âˆ§ Pairwise (Disjoint on s) := by
  have : Pairwise fun i j => âˆƒ (s : {s // s âˆˆ l i}) (t : {t // t âˆˆ l j}), Disjoint s.1 t.1 := by
    simpa only [Pairwise, Function.onFun, Filter.disjoint_iff, exists_prop, Subtype.exists] using hd
  choose! s t hst using this
  refine âŸ¨fun i => â‹‚ j, @s i j âˆ© @t j i, fun i => ?_, fun i j hij => ?_âŸ©
  exacts [iInter_mem.2 fun j => inter_mem (@s i j).2 (@t j i).2,
    (hst hij).mono ((iInter_subset _ j).trans inter_subset_left)
      ((iInter_subset _ i).trans inter_subset_right)]

theorem _root_.Set.PairwiseDisjoint.exists_mem_filter {Î¹ : Type*} {l : Î¹ â†’ Filter Î±} {t : Set Î¹}
    (hd : t.PairwiseDisjoint l) (ht : t.Finite) :
    âˆƒ s : Î¹ â†’ Set Î±, (âˆ€ i, s i âˆˆ l i) âˆ§ t.PairwiseDisjoint s := by
  haveI := ht.to_subtype
  rcases (hd.subtype _ _).exists_mem_filter_of_disjoint with âŸ¨s, hsl, hsdâŸ©
  lift s to (i : t) â†’ {s // s âˆˆ l i} using hsl
  rcases @Subtype.exists_pi_extension Î¹ (fun i => { s // s âˆˆ l i }) _ _ s with âŸ¨s, rflâŸ©
  exact âŸ¨fun i => s i, fun i => (s i).2, hsd.set_of_subtype _ _âŸ©

/-- There is exactly one filter on an empty type. -/
instance unique [IsEmpty Î±] : Unique (Filter Î±) where
  default := âŠ¥
  uniq := filter_eq_bot_of_isEmpty

theorem NeBot.nonempty (f : Filter Î±) [hf : f.NeBot] : Nonempty Î± :=
  not_isEmpty_iff.mp fun _ â†¦ hf.ne (Subsingleton.elim _ _)

/-- There are only two filters on a `Subsingleton`: `âŠ¥` and `âŠ¤`. If the type is empty, then they are
equal. -/
theorem eq_top_of_neBot [Subsingleton Î±] (l : Filter Î±) [NeBot l] : l = âŠ¤ := by
  refine top_unique fun s hs => ?_
  obtain rfl : s = univ := Subsingleton.eq_univ_of_nonempty (nonempty_of_mem hs)
  exact univ_mem

theorem forall_mem_nonempty_iff_neBot {f : Filter Î±} :
    (âˆ€ s : Set Î±, s âˆˆ f â†’ s.Nonempty) â†” NeBot f :=
  âŸ¨fun h => âŸ¨fun hf => not_nonempty_empty (h âˆ… <| hf.symm â–¸ mem_bot)âŸ©, @nonempty_of_mem _ _âŸ©

instance instNontrivialFilter [Nonempty Î±] : Nontrivial (Filter Î±) :=
  âŸ¨âŸ¨âŠ¤, âŠ¥, NeBot.ne <| forall_mem_nonempty_iff_neBot.1
    fun s hs => by rwa [mem_top.1 hs, â† nonempty_iff_univ_nonempty]âŸ©âŸ©

theorem nontrivial_iff_nonempty : Nontrivial (Filter Î±) â†” Nonempty Î± :=
  âŸ¨fun _ =>
    by_contra fun h' =>
      haveI := not_nonempty_iff.1 h'
      not_subsingleton (Filter Î±) inferInstance,
    @Filter.instNontrivialFilter Î±âŸ©

theorem eq_sInf_of_mem_iff_exists_mem {S : Set (Filter Î±)} {l : Filter Î±}
    (h : âˆ€ {s}, s âˆˆ l â†” âˆƒ f âˆˆ S, s âˆˆ f) : l = sInf S :=
  le_antisymm (le_sInf fun f hf _ hs => h.2 âŸ¨f, hf, hsâŸ©)
    fun _ hs => let âŸ¨_, hf, hsâŸ© := h.1 hs; (sInf_le hf) hs

theorem eq_iInf_of_mem_iff_exists_mem {f : Î¹ â†’ Filter Î±} {l : Filter Î±}
    (h : âˆ€ {s}, s âˆˆ l â†” âˆƒ i, s âˆˆ f i) : l = iInf f :=
  eq_sInf_of_mem_iff_exists_mem <| h.trans (exists_range_iff (p := (_ âˆˆ Â·))).symm

theorem eq_biInf_of_mem_iff_exists_mem {f : Î¹ â†’ Filter Î±} {p : Î¹ â†’ Prop} {l : Filter Î±}
    (h : âˆ€ {s}, s âˆˆ l â†” âˆƒ i, p i âˆ§ s âˆˆ f i) : l = â¨… (i) (_ : p i), f i := by
  rw [iInf_subtype']
  exact eq_iInf_of_mem_iff_exists_mem fun {_} => by simp only [Subtype.exists, h, exists_prop]

theorem iInf_sets_eq {f : Î¹ â†’ Filter Î±} (h : Directed (Â· â‰¥ Â·) f) [ne : Nonempty Î¹] :
    (iInf f).sets = â‹ƒ i, (f i).sets :=
  let âŸ¨iâŸ© := ne
  let u :=
    { sets := â‹ƒ i, (f i).sets
      univ_sets := mem_iUnion.2 âŸ¨i, univ_memâŸ©
      sets_of_superset := by
        simp only [mem_iUnion, exists_imp]
        exact fun i hx hxy => âŸ¨i, mem_of_superset hx hxyâŸ©
      inter_sets := by
        simp only [mem_iUnion, exists_imp]
        intro x y a hx b hy
        rcases h a b with âŸ¨c, ha, hbâŸ©
        exact âŸ¨c, inter_mem (ha hx) (hb hy)âŸ© }
  have : u = iInf f := eq_iInf_of_mem_iff_exists_mem mem_iUnion
  -- Porting note: it was just `congr_arg filter.sets this.symm`
  (congr_arg Filter.sets this.symm).trans <| by simp only

theorem mem_iInf_of_directed {f : Î¹ â†’ Filter Î±} (h : Directed (Â· â‰¥ Â·) f) [Nonempty Î¹] (s) :
    s âˆˆ iInf f â†” âˆƒ i, s âˆˆ f i := by
  simp only [â† Filter.mem_sets, iInf_sets_eq h, mem_iUnion]

theorem mem_biInf_of_directed {f : Î² â†’ Filter Î±} {s : Set Î²} (h : DirectedOn (f â»Â¹'o (Â· â‰¥ Â·)) s)
    (ne : s.Nonempty) {t : Set Î±} : (t âˆˆ â¨… i âˆˆ s, f i) â†” âˆƒ i âˆˆ s, t âˆˆ f i := by
  haveI := ne.to_subtype
  simp_rw [iInf_subtype', mem_iInf_of_directed h.directed_val, Subtype.exists, exists_prop]

theorem biInf_sets_eq {f : Î² â†’ Filter Î±} {s : Set Î²} (h : DirectedOn (f â»Â¹'o (Â· â‰¥ Â·)) s)
    (ne : s.Nonempty) : (â¨… i âˆˆ s, f i).sets = â‹ƒ i âˆˆ s, (f i).sets :=
  ext fun t => by simp [mem_biInf_of_directed h ne]

theorem iInf_sets_eq_finite {Î¹ : Type*} (f : Î¹ â†’ Filter Î±) :
    (â¨… i, f i).sets = â‹ƒ t : Finset Î¹, (â¨… i âˆˆ t, f i).sets := by
  rw [iInf_eq_iInf_finset, iInf_sets_eq]
  exact directed_of_isDirected_le fun _ _ => biInf_mono

theorem iInf_sets_eq_finite' (f : Î¹ â†’ Filter Î±) :
    (â¨… i, f i).sets = â‹ƒ t : Finset (PLift Î¹), (â¨… i âˆˆ t, f (PLift.down i)).sets := by
  rw [â† iInf_sets_eq_finite, â† Equiv.plift.surjective.iInf_comp, Equiv.plift_apply]

theorem mem_iInf_finite {Î¹ : Type*} {f : Î¹ â†’ Filter Î±} (s) :
    s âˆˆ iInf f â†” âˆƒ t : Finset Î¹, s âˆˆ â¨… i âˆˆ t, f i :=
  (Set.ext_iff.1 (iInf_sets_eq_finite f) s).trans mem_iUnion

theorem mem_iInf_finite' {f : Î¹ â†’ Filter Î±} (s) :
    s âˆˆ iInf f â†” âˆƒ t : Finset (PLift Î¹), s âˆˆ â¨… i âˆˆ t, f (PLift.down i) :=
  (Set.ext_iff.1 (iInf_sets_eq_finite' f) s).trans mem_iUnion

@[simp]
theorem sup_join {fâ‚ fâ‚‚ : Filter (Filter Î±)} : join fâ‚ âŠ” join fâ‚‚ = join (fâ‚ âŠ” fâ‚‚) :=
  Filter.ext fun x => by simp only [mem_sup, mem_join]

@[simp]
theorem iSup_join {Î¹ : Sort w} {f : Î¹ â†’ Filter (Filter Î±)} : â¨† x, join (f x) = join (â¨† x, f x) :=
  Filter.ext fun x => by simp only [mem_iSup, mem_join]

instance : DistribLattice (Filter Î±) :=
  { Filter.instCompleteLatticeFilter with
    le_sup_inf := by
      intro x y z s
      simp only [and_assoc, mem_inf_iff, mem_sup, exists_prop, exists_imp, and_imp]
      rintro hs tâ‚ htâ‚ tâ‚‚ htâ‚‚ rfl
      exact
        âŸ¨tâ‚, x.sets_of_superset hs inter_subset_left, htâ‚, tâ‚‚,
          x.sets_of_superset hs inter_subset_right, htâ‚‚, rflâŸ© }

/-- The dual version does not hold! `Filter Î±` is not a `CompleteDistribLattice`. -/
def coframeMinimalAxioms : Coframe.MinimalAxioms (Filter Î±) :=
  { Filter.instCompleteLatticeFilter with
    iInf_sup_le_sup_sInf := fun f s t âŸ¨hâ‚, hâ‚‚âŸ© => by
      classical
      rw [iInf_subtype']
      rw [sInf_eq_iInf', iInf_sets_eq_finite, mem_iUnion] at hâ‚‚
      obtain âŸ¨u, huâŸ© := hâ‚‚
      rw [â† Finset.inf_eq_iInf] at hu
      suffices â¨… i : s, f âŠ” â†‘i â‰¤ f âŠ” u.inf fun i => â†‘i from this âŸ¨hâ‚, huâŸ©
      refine Finset.induction_on u (le_sup_of_le_right le_top) ?_
      rintro âŸ¨iâŸ© u _ ih
      rw [Finset.inf_insert, sup_inf_left]
      exact le_inf (iInf_le _ _) ih }

instance instCoframe : Coframe (Filter Î±) := .ofMinimalAxioms coframeMinimalAxioms

theorem mem_iInf_finset {s : Finset Î±} {f : Î± â†’ Filter Î²} {t : Set Î²} :
    (t âˆˆ â¨… a âˆˆ s, f a) â†” âˆƒ p : Î± â†’ Set Î², (âˆ€ a âˆˆ s, p a âˆˆ f a) âˆ§ t = â‹‚ a âˆˆ s, p a := by
  classical
  simp only [â† Finset.set_biInter_coe, biInter_eq_iInter, iInf_subtype']
  refine âŸ¨fun h => ?_, ?_âŸ©
  Â· rcases (mem_iInf_of_finite _).1 h with âŸ¨p, hp, rflâŸ©
    refine âŸ¨fun a => if h : a âˆˆ s then p âŸ¨a, hâŸ© else univ,
            fun a ha => by simpa [ha] using hp âŸ¨a, haâŸ©, ?_âŸ©
    refine iInter_congr_of_surjective id surjective_id ?_
    rintro âŸ¨a, haâŸ©
    simp [ha]
  Â· rintro âŸ¨p, hpf, rflâŸ©
    exact iInter_mem.2 fun a => mem_iInf_of_mem a (hpf a a.2)

/-- If `f : Î¹ â†’ Filter Î±` is directed, `Î¹` is not empty, and `âˆ€ i, f i â‰  âŠ¥`, then `iInf f â‰  âŠ¥`.
See also `iInf_neBot_of_directed` for a version assuming `Nonempty Î±` instead of `Nonempty Î¹`. -/
theorem iInf_neBot_of_directed' {f : Î¹ â†’ Filter Î±} [Nonempty Î¹] (hd : Directed (Â· â‰¥ Â·) f) :
    (âˆ€ i, NeBot (f i)) â†’ NeBot (iInf f) :=
  not_imp_not.1 <| by simpa only [not_forall, not_neBot, â† empty_mem_iff_bot,
    mem_iInf_of_directed hd] using id

/-- If `f : Î¹ â†’ Filter Î±` is directed, `Î±` is not empty, and `âˆ€ i, f i â‰  âŠ¥`, then `iInf f â‰  âŠ¥`.
See also `iInf_neBot_of_directed'` for a version assuming `Nonempty Î¹` instead of `Nonempty Î±`. -/
theorem iInf_neBot_of_directed {f : Î¹ â†’ Filter Î±} [hn : Nonempty Î±] (hd : Directed (Â· â‰¥ Â·) f)
    (hb : âˆ€ i, NeBot (f i)) : NeBot (iInf f) := by
  cases isEmpty_or_nonempty Î¹
  Â· constructor
    simp [iInf_of_empty f, top_ne_bot]
  Â· exact iInf_neBot_of_directed' hd hb

theorem sInf_neBot_of_directed' {s : Set (Filter Î±)} (hne : s.Nonempty) (hd : DirectedOn (Â· â‰¥ Â·) s)
    (hbot : âŠ¥ âˆ‰ s) : NeBot (sInf s) :=
  (sInf_eq_iInf' s).symm â–¸
    @iInf_neBot_of_directed' _ _ _ hne.to_subtype hd.directed_val fun âŸ¨_, hfâŸ© =>
      âŸ¨ne_of_mem_of_not_mem hf hbotâŸ©

theorem sInf_neBot_of_directed [Nonempty Î±] {s : Set (Filter Î±)} (hd : DirectedOn (Â· â‰¥ Â·) s)
    (hbot : âŠ¥ âˆ‰ s) : NeBot (sInf s) :=
  (sInf_eq_iInf' s).symm â–¸
    iInf_neBot_of_directed hd.directed_val fun âŸ¨_, hfâŸ© => âŸ¨ne_of_mem_of_not_mem hf hbotâŸ©

theorem iInf_neBot_iff_of_directed' {f : Î¹ â†’ Filter Î±} [Nonempty Î¹] (hd : Directed (Â· â‰¥ Â·) f) :
    NeBot (iInf f) â†” âˆ€ i, NeBot (f i) :=
  âŸ¨fun H i => H.mono (iInf_le _ i), iInf_neBot_of_directed' hdâŸ©

theorem iInf_neBot_iff_of_directed {f : Î¹ â†’ Filter Î±} [Nonempty Î±] (hd : Directed (Â· â‰¥ Â·) f) :
    NeBot (iInf f) â†” âˆ€ i, NeBot (f i) :=
  âŸ¨fun H i => H.mono (iInf_le _ i), iInf_neBot_of_directed hdâŸ©

@[elab_as_elim]
theorem iInf_sets_induct {f : Î¹ â†’ Filter Î±} {s : Set Î±} (hs : s âˆˆ iInf f) {p : Set Î± â†’ Prop}
    (uni : p univ) (ins : âˆ€ {i sâ‚ sâ‚‚}, sâ‚ âˆˆ f i â†’ p sâ‚‚ â†’ p (sâ‚ âˆ© sâ‚‚)) : p s := by
  classical
  rw [mem_iInf_finite'] at hs
  simp only [â† Finset.inf_eq_iInf] at hs
  rcases hs with âŸ¨is, hisâŸ©
  induction is using Finset.induction_on generalizing s with
  | empty => rwa [mem_top.1 his]
  | insert _ ih =>
    rw [Finset.inf_insert, mem_inf_iff] at his
    rcases his with âŸ¨sâ‚, hsâ‚, sâ‚‚, hsâ‚‚, rflâŸ©
    exact ins hsâ‚ (ih hsâ‚‚)

/-! #### `principal` equations -/

@[simp]
theorem inf_principal {s t : Set Î±} : ğ“Ÿ s âŠ“ ğ“Ÿ t = ğ“Ÿ (s âˆ© t) :=
  le_antisymm
    (by simp only [le_principal_iff, mem_inf_iff]; exact âŸ¨s, Subset.rfl, t, Subset.rfl, rflâŸ©)
    (by simp [le_inf_iff, inter_subset_left, inter_subset_right])

@[simp]
theorem sup_principal {s t : Set Î±} : ğ“Ÿ s âŠ” ğ“Ÿ t = ğ“Ÿ (s âˆª t) :=
  Filter.ext fun u => by simp only [union_subset_iff, mem_sup, mem_principal]

@[simp]
theorem iSup_principal {Î¹ : Sort w} {s : Î¹ â†’ Set Î±} : â¨† x, ğ“Ÿ (s x) = ğ“Ÿ (â‹ƒ i, s i) :=
  Filter.ext fun x => by simp only [mem_iSup, mem_principal, iUnion_subset_iff]

@[simp]
theorem principal_eq_bot_iff {s : Set Î±} : ğ“Ÿ s = âŠ¥ â†” s = âˆ… :=
  empty_mem_iff_bot.symm.trans <| mem_principal.trans subset_empty_iff

@[simp]
theorem principal_neBot_iff {s : Set Î±} : NeBot (ğ“Ÿ s) â†” s.Nonempty :=
  neBot_iff.trans <| (not_congr principal_eq_bot_iff).trans nonempty_iff_ne_empty.symm

alias âŸ¨_, _root_.Set.Nonempty.principal_neBotâŸ© := principal_neBot_iff

theorem isCompl_principal (s : Set Î±) : IsCompl (ğ“Ÿ s) (ğ“Ÿ sá¶œ) :=
  IsCompl.of_eq (by rw [inf_principal, inter_compl_self, principal_empty]) <| by
    rw [sup_principal, union_compl_self, principal_univ]

theorem mem_inf_principal' {f : Filter Î±} {s t : Set Î±} : s âˆˆ f âŠ“ ğ“Ÿ t â†” tá¶œ âˆª s âˆˆ f := by
  simp only [â† le_principal_iff, (isCompl_principal s).le_left_iff, disjoint_assoc, inf_principal,
    â† (isCompl_principal (t âˆ© sá¶œ)).le_right_iff, compl_inter, compl_compl]

lemma mem_inf_principal {f : Filter Î±} {s t : Set Î±} : s âˆˆ f âŠ“ ğ“Ÿ t â†” { x | x âˆˆ t â†’ x âˆˆ s } âˆˆ f := by
  simp only [mem_inf_principal', imp_iff_not_or, setOf_or, compl_def, setOf_mem_eq]

lemma iSup_inf_principal (f : Î¹ â†’ Filter Î±) (s : Set Î±) : â¨† i, f i âŠ“ ğ“Ÿ s = (â¨† i, f i) âŠ“ ğ“Ÿ s := by
  ext
  simp only [mem_iSup, mem_inf_principal]

theorem inf_principal_eq_bot {f : Filter Î±} {s : Set Î±} : f âŠ“ ğ“Ÿ s = âŠ¥ â†” sá¶œ âˆˆ f := by
  rw [â† empty_mem_iff_bot, mem_inf_principal]
  simp only [mem_empty_iff_false, imp_false, compl_def]

theorem mem_of_eq_bot {f : Filter Î±} {s : Set Î±} (h : f âŠ“ ğ“Ÿ sá¶œ = âŠ¥) : s âˆˆ f := by
  rwa [inf_principal_eq_bot, compl_compl] at h

theorem diff_mem_inf_principal_compl {f : Filter Î±} {s : Set Î±} (hs : s âˆˆ f) (t : Set Î±) :
    s \ t âˆˆ f âŠ“ ğ“Ÿ tá¶œ :=
  inter_mem_inf hs <| mem_principal_self tá¶œ

theorem principal_le_iff {s : Set Î±} {f : Filter Î±} : ğ“Ÿ s â‰¤ f â†” âˆ€ V âˆˆ f, s âŠ† V := by
  simp_rw [le_def, mem_principal]

@[simp]
theorem iInf_principal_finset {Î¹ : Type w} (s : Finset Î¹) (f : Î¹ â†’ Set Î±) :
    â¨… i âˆˆ s, ğ“Ÿ (f i) = ğ“Ÿ (â‹‚ i âˆˆ s, f i) := by
  classical
  induction' s using Finset.induction_on with i s _ hs
  Â· simp
  Â· rw [Finset.iInf_insert, Finset.set_biInter_insert, hs, inf_principal]

theorem iInf_principal {Î¹ : Sort w} [Finite Î¹] (f : Î¹ â†’ Set Î±) : â¨… i, ğ“Ÿ (f i) = ğ“Ÿ (â‹‚ i, f i) := by
  cases nonempty_fintype (PLift Î¹)
  rw [â† iInf_plift_down, â† iInter_plift_down]
  simpa using iInf_principal_finset Finset.univ (f <| PLift.down Â·)

/-- A special case of `iInf_principal` that is safe to mark `simp`. -/
@[simp]
theorem iInf_principal' {Î¹ : Type w} [Finite Î¹] (f : Î¹ â†’ Set Î±) : â¨… i, ğ“Ÿ (f i) = ğ“Ÿ (â‹‚ i, f i) :=
  iInf_principal _

theorem iInf_principal_finite {Î¹ : Type w} {s : Set Î¹} (hs : s.Finite) (f : Î¹ â†’ Set Î±) :
    â¨… i âˆˆ s, ğ“Ÿ (f i) = ğ“Ÿ (â‹‚ i âˆˆ s, f i) := by
  lift s to Finset Î¹ using hs
  exact mod_cast iInf_principal_finset s f

end Lattice

@[mono, gcongr]
theorem join_mono {fâ‚ fâ‚‚ : Filter (Filter Î±)} (h : fâ‚ â‰¤ fâ‚‚) : join fâ‚ â‰¤ join fâ‚‚ := fun _ hs => h hs

/-! ### Eventually -/

/-- `f.Eventually p` or `âˆ€á¶  x in f, p x` mean that `{x | p x} âˆˆ f`. E.g., `âˆ€á¶  x in atTop, p x`
means that `p` holds true for sufficiently large `x`. -/
protected def Eventually (p : Î± â†’ Prop) (f : Filter Î±) : Prop :=
  { x | p x } âˆˆ f

@[inherit_doc Filter.Eventually]
notation3 "âˆ€á¶  "(...)" in "f", "r:(scoped p => Filter.Eventually p f) => r

theorem eventually_iff {f : Filter Î±} {P : Î± â†’ Prop} : (âˆ€á¶  x in f, P x) â†” { x | P x } âˆˆ f :=
  Iff.rfl

@[simp]
theorem eventually_mem_set {s : Set Î±} {l : Filter Î±} : (âˆ€á¶  x in l, x âˆˆ s) â†” s âˆˆ l :=
  Iff.rfl

protected theorem ext' {fâ‚ fâ‚‚ : Filter Î±}
    (h : âˆ€ p : Î± â†’ Prop, (âˆ€á¶  x in fâ‚, p x) â†” âˆ€á¶  x in fâ‚‚, p x) : fâ‚ = fâ‚‚ :=
  Filter.ext h

theorem Eventually.filter_mono {fâ‚ fâ‚‚ : Filter Î±} (h : fâ‚ â‰¤ fâ‚‚) {p : Î± â†’ Prop}
    (hp : âˆ€á¶  x in fâ‚‚, p x) : âˆ€á¶  x in fâ‚, p x :=
  h hp

theorem eventually_of_mem {f : Filter Î±} {P : Î± â†’ Prop} {U : Set Î±} (hU : U âˆˆ f)
    (h : âˆ€ x âˆˆ U, P x) : âˆ€á¶  x in f, P x :=
  mem_of_superset hU h

protected theorem Eventually.and {p q : Î± â†’ Prop} {f : Filter Î±} :
    f.Eventually p â†’ f.Eventually q â†’ âˆ€á¶  x in f, p x âˆ§ q x :=
  inter_mem

@[simp] theorem eventually_true (f : Filter Î±) : âˆ€á¶  _ in f, True := univ_mem

theorem Eventually.of_forall {p : Î± â†’ Prop} {f : Filter Î±} (hp : âˆ€ x, p x) : âˆ€á¶  x in f, p x :=
  univ_mem' hp

@[deprecated (since := "2024-08-02")] alias eventually_of_forall := Eventually.of_forall

@[simp]
theorem eventually_false_iff_eq_bot {f : Filter Î±} : (âˆ€á¶  _ in f, False) â†” f = âŠ¥ :=
  empty_mem_iff_bot

@[simp]
theorem eventually_const {f : Filter Î±} [t : NeBot f] {p : Prop} : (âˆ€á¶  _ in f, p) â†” p := by
  by_cases h : p <;> simp [h, t.ne]

theorem eventually_iff_exists_mem {p : Î± â†’ Prop} {f : Filter Î±} :
    (âˆ€á¶  x in f, p x) â†” âˆƒ v âˆˆ f, âˆ€ y âˆˆ v, p y :=
  exists_mem_subset_iff.symm

theorem Eventually.exists_mem {p : Î± â†’ Prop} {f : Filter Î±} (hp : âˆ€á¶  x in f, p x) :
    âˆƒ v âˆˆ f, âˆ€ y âˆˆ v, p y :=
  eventually_iff_exists_mem.1 hp

theorem Eventually.mp {p q : Î± â†’ Prop} {f : Filter Î±} (hp : âˆ€á¶  x in f, p x)
    (hq : âˆ€á¶  x in f, p x â†’ q x) : âˆ€á¶  x in f, q x :=
  mp_mem hp hq

theorem Eventually.mono {p q : Î± â†’ Prop} {f : Filter Î±} (hp : âˆ€á¶  x in f, p x)
    (hq : âˆ€ x, p x â†’ q x) : âˆ€á¶  x in f, q x :=
  hp.mp (Eventually.of_forall hq)

theorem forall_eventually_of_eventually_forall {f : Filter Î±} {p : Î± â†’ Î² â†’ Prop}
    (h : âˆ€á¶  x in f, âˆ€ y, p x y) : âˆ€ y, âˆ€á¶  x in f, p x y :=
  fun y => h.mono fun _ h => h y

@[simp]
theorem eventually_and {p q : Î± â†’ Prop} {f : Filter Î±} :
    (âˆ€á¶  x in f, p x âˆ§ q x) â†” (âˆ€á¶  x in f, p x) âˆ§ âˆ€á¶  x in f, q x :=
  inter_mem_iff

theorem Eventually.congr {f : Filter Î±} {p q : Î± â†’ Prop} (h' : âˆ€á¶  x in f, p x)
    (h : âˆ€á¶  x in f, p x â†” q x) : âˆ€á¶  x in f, q x :=
  h'.mp (h.mono fun _ hx => hx.mp)

theorem eventually_congr {f : Filter Î±} {p q : Î± â†’ Prop} (h : âˆ€á¶  x in f, p x â†” q x) :
    (âˆ€á¶  x in f, p x) â†” âˆ€á¶  x in f, q x :=
  âŸ¨fun hp => hp.congr h, fun hq => hq.congr <| by simpa only [Iff.comm] using hâŸ©

@[simp]
theorem eventually_all {Î¹ : Sort*} [Finite Î¹] {l} {p : Î¹ â†’ Î± â†’ Prop} :
    (âˆ€á¶  x in l, âˆ€ i, p i x) â†” âˆ€ i, âˆ€á¶  x in l, p i x := by
  simpa only [Filter.Eventually, setOf_forall] using iInter_mem

@[simp]
theorem eventually_all_finite {Î¹} {I : Set Î¹} (hI : I.Finite) {l} {p : Î¹ â†’ Î± â†’ Prop} :
    (âˆ€á¶  x in l, âˆ€ i âˆˆ I, p i x) â†” âˆ€ i âˆˆ I, âˆ€á¶  x in l, p i x := by
  simpa only [Filter.Eventually, setOf_forall] using biInter_mem hI

alias _root_.Set.Finite.eventually_all := eventually_all_finite

-- attribute [protected] Set.Finite.eventually_all

@[simp] theorem eventually_all_finset {Î¹} (I : Finset Î¹) {l} {p : Î¹ â†’ Î± â†’ Prop} :
    (âˆ€á¶  x in l, âˆ€ i âˆˆ I, p i x) â†” âˆ€ i âˆˆ I, âˆ€á¶  x in l, p i x :=
  I.finite_toSet.eventually_all

alias _root_.Finset.eventually_all := eventually_all_finset

-- attribute [protected] Finset.eventually_all

@[simp]
theorem eventually_or_distrib_left {f : Filter Î±} {p : Prop} {q : Î± â†’ Prop} :
    (âˆ€á¶  x in f, p âˆ¨ q x) â†” p âˆ¨ âˆ€á¶  x in f, q x :=
  by_cases (fun h : p => by simp [h]) fun h => by simp [h]

@[simp]
theorem eventually_or_distrib_right {f : Filter Î±} {p : Î± â†’ Prop} {q : Prop} :
    (âˆ€á¶  x in f, p x âˆ¨ q) â†” (âˆ€á¶  x in f, p x) âˆ¨ q := by
  simp only [@or_comm _ q, eventually_or_distrib_left]

theorem eventually_imp_distrib_left {f : Filter Î±} {p : Prop} {q : Î± â†’ Prop} :
    (âˆ€á¶  x in f, p â†’ q x) â†” p â†’ âˆ€á¶  x in f, q x :=
  eventually_all

@[simp]
theorem eventually_bot {p : Î± â†’ Prop} : âˆ€á¶  x in âŠ¥, p x :=
  âŸ¨âŸ©

@[simp]
theorem eventually_top {p : Î± â†’ Prop} : (âˆ€á¶  x in âŠ¤, p x) â†” âˆ€ x, p x :=
  Iff.rfl

@[simp]
theorem eventually_sup {p : Î± â†’ Prop} {f g : Filter Î±} :
    (âˆ€á¶  x in f âŠ” g, p x) â†” (âˆ€á¶  x in f, p x) âˆ§ âˆ€á¶  x in g, p x :=
  Iff.rfl

@[simp]
theorem eventually_sSup {p : Î± â†’ Prop} {fs : Set (Filter Î±)} :
    (âˆ€á¶  x in sSup fs, p x) â†” âˆ€ f âˆˆ fs, âˆ€á¶  x in f, p x :=
  Iff.rfl

@[simp]
theorem eventually_iSup {p : Î± â†’ Prop} {fs : Î¹ â†’ Filter Î±} :
    (âˆ€á¶  x in â¨† b, fs b, p x) â†” âˆ€ b, âˆ€á¶  x in fs b, p x :=
  mem_iSup

@[simp]
theorem eventually_principal {a : Set Î±} {p : Î± â†’ Prop} : (âˆ€á¶  x in ğ“Ÿ a, p x) â†” âˆ€ x âˆˆ a, p x :=
  Iff.rfl

theorem Eventually.forall_mem {Î± : Type*} {f : Filter Î±} {s : Set Î±} {P : Î± â†’ Prop}
    (hP : âˆ€á¶  x in f, P x) (hf : ğ“Ÿ s â‰¤ f) : âˆ€ x âˆˆ s, P x :=
  Filter.eventually_principal.mp (hP.filter_mono hf)

theorem eventually_inf {f g : Filter Î±} {p : Î± â†’ Prop} :
    (âˆ€á¶  x in f âŠ“ g, p x) â†” âˆƒ s âˆˆ f, âˆƒ t âˆˆ g, âˆ€ x âˆˆ s âˆ© t, p x :=
  mem_inf_iff_superset

theorem eventually_inf_principal {f : Filter Î±} {p : Î± â†’ Prop} {s : Set Î±} :
    (âˆ€á¶  x in f âŠ“ ğ“Ÿ s, p x) â†” âˆ€á¶  x in f, x âˆˆ s â†’ p x :=
  mem_inf_principal

/-! ### Frequently -/

/-- `f.Frequently p` or `âˆƒá¶  x in f, p x` mean that `{x | Â¬p x} âˆ‰ f`. E.g., `âˆƒá¶  x in atTop, p x`
means that there exist arbitrarily large `x` for which `p` holds true. -/
protected def Frequently (p : Î± â†’ Prop) (f : Filter Î±) : Prop :=
  Â¬âˆ€á¶  x in f, Â¬p x

@[inherit_doc Filter.Frequently]
notation3 "âˆƒá¶  "(...)" in "f", "r:(scoped p => Filter.Frequently p f) => r

theorem Eventually.frequently {f : Filter Î±} [NeBot f] {p : Î± â†’ Prop} (h : âˆ€á¶  x in f, p x) :
    âˆƒá¶  x in f, p x :=
  compl_not_mem h

theorem Frequently.of_forall {f : Filter Î±} [NeBot f] {p : Î± â†’ Prop} (h : âˆ€ x, p x) :
    âˆƒá¶  x in f, p x :=
  Eventually.frequently (Eventually.of_forall h)

@[deprecated (since := "2024-08-02")] alias frequently_of_forall := Frequently.of_forall

theorem Frequently.mp {p q : Î± â†’ Prop} {f : Filter Î±} (h : âˆƒá¶  x in f, p x)
    (hpq : âˆ€á¶  x in f, p x â†’ q x) : âˆƒá¶  x in f, q x :=
  mt (fun hq => hq.mp <| hpq.mono fun _ => mt) h

theorem Frequently.filter_mono {p : Î± â†’ Prop} {f g : Filter Î±} (h : âˆƒá¶  x in f, p x) (hle : f â‰¤ g) :
    âˆƒá¶  x in g, p x :=
  mt (fun h' => h'.filter_mono hle) h

theorem Frequently.mono {p q : Î± â†’ Prop} {f : Filter Î±} (h : âˆƒá¶  x in f, p x)
    (hpq : âˆ€ x, p x â†’ q x) : âˆƒá¶  x in f, q x :=
  h.mp (Eventually.of_forall hpq)

theorem Frequently.and_eventually {p q : Î± â†’ Prop} {f : Filter Î±} (hp : âˆƒá¶  x in f, p x)
    (hq : âˆ€á¶  x in f, q x) : âˆƒá¶  x in f, p x âˆ§ q x := by
  refine mt (fun h => hq.mp <| h.mono ?_) hp
  exact fun x hpq hq hp => hpq âŸ¨hp, hqâŸ©

theorem Eventually.and_frequently {p q : Î± â†’ Prop} {f : Filter Î±} (hp : âˆ€á¶  x in f, p x)
    (hq : âˆƒá¶  x in f, q x) : âˆƒá¶  x in f, p x âˆ§ q x := by
  simpa only [and_comm] using hq.and_eventually hp

theorem Frequently.exists {p : Î± â†’ Prop} {f : Filter Î±} (hp : âˆƒá¶  x in f, p x) : âˆƒ x, p x := by
  by_contra H
  replace H : âˆ€á¶  x in f, Â¬p x := Eventually.of_forall (not_exists.1 H)
  exact hp H

theorem Eventually.exists {p : Î± â†’ Prop} {f : Filter Î±} [NeBot f] (hp : âˆ€á¶  x in f, p x) :
    âˆƒ x, p x :=
  hp.frequently.exists

lemma frequently_iff_neBot {l : Filter Î±} {p : Î± â†’ Prop} :
    (âˆƒá¶  x in l, p x) â†” NeBot (l âŠ“ ğ“Ÿ {x | p x}) := by
  rw [neBot_iff, Ne, inf_principal_eq_bot]; rfl

lemma frequently_mem_iff_neBot {l : Filter Î±} {s : Set Î±} : (âˆƒá¶  x in l, x âˆˆ s) â†” NeBot (l âŠ“ ğ“Ÿ s) :=
  frequently_iff_neBot

theorem frequently_iff_forall_eventually_exists_and {p : Î± â†’ Prop} {f : Filter Î±} :
    (âˆƒá¶  x in f, p x) â†” âˆ€ {q : Î± â†’ Prop}, (âˆ€á¶  x in f, q x) â†’ âˆƒ x, p x âˆ§ q x :=
  âŸ¨fun hp q hq => (hp.and_eventually hq).exists, fun H hp => by
    simpa only [and_not_self_iff, exists_false] using H hpâŸ©

theorem frequently_iff {f : Filter Î±} {P : Î± â†’ Prop} :
    (âˆƒá¶  x in f, P x) â†” âˆ€ {U}, U âˆˆ f â†’ âˆƒ x âˆˆ U, P x := by
  simp only [frequently_iff_forall_eventually_exists_and, @and_comm (P _)]
  rfl

@[simp]
theorem not_eventually {p : Î± â†’ Prop} {f : Filter Î±} : (Â¬âˆ€á¶  x in f, p x) â†” âˆƒá¶  x in f, Â¬p x := by
  simp [Filter.Frequently]

@[simp]
theorem not_frequently {p : Î± â†’ Prop} {f : Filter Î±} : (Â¬âˆƒá¶  x in f, p x) â†” âˆ€á¶  x in f, Â¬p x := by
  simp only [Filter.Frequently, not_not]

@[simp]
theorem frequently_true_iff_neBot (f : Filter Î±) : (âˆƒá¶  _ in f, True) â†” NeBot f := by
  simp [frequently_iff_neBot]

@[simp]
theorem frequently_false (f : Filter Î±) : Â¬âˆƒá¶  _ in f, False := by simp

@[simp]
theorem frequently_const {f : Filter Î±} [NeBot f] {p : Prop} : (âˆƒá¶  _ in f, p) â†” p := by
  by_cases p <;> simp [*]

@[simp]
theorem frequently_or_distrib {f : Filter Î±} {p q : Î± â†’ Prop} :
    (âˆƒá¶  x in f, p x âˆ¨ q x) â†” (âˆƒá¶  x in f, p x) âˆ¨ âˆƒá¶  x in f, q x := by
  simp only [Filter.Frequently, â† not_and_or, not_or, eventually_and]

theorem frequently_or_distrib_left {f : Filter Î±} [NeBot f] {p : Prop} {q : Î± â†’ Prop} :
    (âˆƒá¶  x in f, p âˆ¨ q x) â†” p âˆ¨ âˆƒá¶  x in f, q x := by simp

theorem frequently_or_distrib_right {f : Filter Î±} [NeBot f] {p : Î± â†’ Prop} {q : Prop} :
    (âˆƒá¶  x in f, p x âˆ¨ q) â†” (âˆƒá¶  x in f, p x) âˆ¨ q := by simp

theorem frequently_imp_distrib {f : Filter Î±} {p q : Î± â†’ Prop} :
    (âˆƒá¶  x in f, p x â†’ q x) â†” (âˆ€á¶  x in f, p x) â†’ âˆƒá¶  x in f, q x := by
  simp [imp_iff_not_or]

theorem frequently_imp_distrib_left {f : Filter Î±} [NeBot f] {p : Prop} {q : Î± â†’ Prop} :
    (âˆƒá¶  x in f, p â†’ q x) â†” p â†’ âˆƒá¶  x in f, q x := by simp [frequently_imp_distrib]

theorem frequently_imp_distrib_right {f : Filter Î±} [NeBot f] {p : Î± â†’ Prop} {q : Prop} :
    (âˆƒá¶  x in f, p x â†’ q) â†” (âˆ€á¶  x in f, p x) â†’ q := by
  set_option tactic.skipAssignedInstances false in simp [frequently_imp_distrib]

theorem eventually_imp_distrib_right {f : Filter Î±} {p : Î± â†’ Prop} {q : Prop} :
    (âˆ€á¶  x in f, p x â†’ q) â†” (âˆƒá¶  x in f, p x) â†’ q := by
  simp only [imp_iff_not_or, eventually_or_distrib_right, not_frequently]

@[simp]
theorem frequently_and_distrib_left {f : Filter Î±} {p : Prop} {q : Î± â†’ Prop} :
    (âˆƒá¶  x in f, p âˆ§ q x) â†” p âˆ§ âˆƒá¶  x in f, q x := by
  simp only [Filter.Frequently, not_and, eventually_imp_distrib_left, Classical.not_imp]

@[simp]
theorem frequently_and_distrib_right {f : Filter Î±} {p : Î± â†’ Prop} {q : Prop} :
    (âˆƒá¶  x in f, p x âˆ§ q) â†” (âˆƒá¶  x in f, p x) âˆ§ q := by
  simp only [@and_comm _ q, frequently_and_distrib_left]

@[simp]
theorem frequently_bot {p : Î± â†’ Prop} : Â¬âˆƒá¶  x in âŠ¥, p x := by simp

@[simp]
theorem frequently_top {p : Î± â†’ Prop} : (âˆƒá¶  x in âŠ¤, p x) â†” âˆƒ x, p x := by simp [Filter.Frequently]

@[simp]
theorem frequently_principal {a : Set Î±} {p : Î± â†’ Prop} : (âˆƒá¶  x in ğ“Ÿ a, p x) â†” âˆƒ x âˆˆ a, p x := by
  simp [Filter.Frequently, not_forall]

theorem frequently_inf_principal {f : Filter Î±} {s : Set Î±} {p : Î± â†’ Prop} :
    (âˆƒá¶  x in f âŠ“ ğ“Ÿ s, p x) â†” âˆƒá¶  x in f, x âˆˆ s âˆ§ p x := by
  simp only [Filter.Frequently, eventually_inf_principal, not_and]

alias âŸ¨Frequently.of_inf_principal, Frequently.inf_principalâŸ© := frequently_inf_principal

theorem frequently_sup {p : Î± â†’ Prop} {f g : Filter Î±} :
    (âˆƒá¶  x in f âŠ” g, p x) â†” (âˆƒá¶  x in f, p x) âˆ¨ âˆƒá¶  x in g, p x := by
  simp only [Filter.Frequently, eventually_sup, not_and_or]

@[simp]
theorem frequently_sSup {p : Î± â†’ Prop} {fs : Set (Filter Î±)} :
    (âˆƒá¶  x in sSup fs, p x) â†” âˆƒ f âˆˆ fs, âˆƒá¶  x in f, p x := by
  simp only [Filter.Frequently, not_forall, eventually_sSup, exists_prop]

@[simp]
theorem frequently_iSup {p : Î± â†’ Prop} {fs : Î² â†’ Filter Î±} :
    (âˆƒá¶  x in â¨† b, fs b, p x) â†” âˆƒ b, âˆƒá¶  x in fs b, p x := by
  simp only [Filter.Frequently, eventually_iSup, not_forall]

theorem Eventually.choice {r : Î± â†’ Î² â†’ Prop} {l : Filter Î±} [l.NeBot] (h : âˆ€á¶  x in l, âˆƒ y, r x y) :
    âˆƒ f : Î± â†’ Î², âˆ€á¶  x in l, r x (f x) := by
  haveI : Nonempty Î² := let âŸ¨_, hxâŸ© := h.exists; hx.nonempty
  choose! f hf using fun x (hx : âˆƒ y, r x y) => hx
  exact âŸ¨f, h.mono hfâŸ©

/-!
### Relation â€œeventually equalâ€
-/

/-- Two functions `f` and `g` are *eventually equal* along a filter `l` if the set of `x` such that
`f x = g x` belongs to `l`. -/
def EventuallyEq (l : Filter Î±) (f g : Î± â†’ Î²) : Prop :=
  âˆ€á¶  x in l, f x = g x

@[inherit_doc]
notation:50 f " =á¶ [" l:50 "] " g:50 => EventuallyEq l f g

theorem EventuallyEq.eventually {l : Filter Î±} {f g : Î± â†’ Î²} (h : f =á¶ [l] g) :
    âˆ€á¶  x in l, f x = g x :=
  h

theorem EventuallyEq.rw {l : Filter Î±} {f g : Î± â†’ Î²} (h : f =á¶ [l] g) (p : Î± â†’ Î² â†’ Prop)
    (hf : âˆ€á¶  x in l, p x (f x)) : âˆ€á¶  x in l, p x (g x) :=
  hf.congr <| h.mono fun _ hx => hx â–¸ Iff.rfl

theorem eventuallyEq_set {s t : Set Î±} {l : Filter Î±} : s =á¶ [l] t â†” âˆ€á¶  x in l, x âˆˆ s â†” x âˆˆ t :=
  eventually_congr <| Eventually.of_forall fun _ â†¦ eq_iff_iff

alias âŸ¨EventuallyEq.mem_iff, Eventually.set_eqâŸ© := eventuallyEq_set

@[simp]
theorem eventuallyEq_univ {s : Set Î±} {l : Filter Î±} : s =á¶ [l] univ â†” s âˆˆ l := by
  simp [eventuallyEq_set]

theorem EventuallyEq.exists_mem {l : Filter Î±} {f g : Î± â†’ Î²} (h : f =á¶ [l] g) :
    âˆƒ s âˆˆ l, EqOn f g s :=
  Eventually.exists_mem h

theorem eventuallyEq_of_mem {l : Filter Î±} {f g : Î± â†’ Î²} {s : Set Î±} (hs : s âˆˆ l) (h : EqOn f g s) :
    f =á¶ [l] g :=
  eventually_of_mem hs h

theorem eventuallyEq_iff_exists_mem {l : Filter Î±} {f g : Î± â†’ Î²} :
    f =á¶ [l] g â†” âˆƒ s âˆˆ l, EqOn f g s :=
  eventually_iff_exists_mem

theorem EventuallyEq.filter_mono {l l' : Filter Î±} {f g : Î± â†’ Î²} (hâ‚ : f =á¶ [l] g) (hâ‚‚ : l' â‰¤ l) :
    f =á¶ [l'] g :=
  hâ‚‚ hâ‚

@[refl, simp]
theorem EventuallyEq.refl (l : Filter Î±) (f : Î± â†’ Î²) : f =á¶ [l] f :=
  Eventually.of_forall fun _ => rfl

protected theorem EventuallyEq.rfl {l : Filter Î±} {f : Î± â†’ Î²} : f =á¶ [l] f :=
  EventuallyEq.refl l f

theorem EventuallyEq.of_eq {l : Filter Î±} {f g : Î± â†’ Î²} (h : f = g) : f =á¶ [l] g := h â–¸ .rfl
alias _root_.Eq.eventuallyEq := EventuallyEq.of_eq

@[symm]
theorem EventuallyEq.symm {f g : Î± â†’ Î²} {l : Filter Î±} (H : f =á¶ [l] g) : g =á¶ [l] f :=
  H.mono fun _ => Eq.symm

lemma eventuallyEq_comm {f g : Î± â†’ Î²} {l : Filter Î±} : f =á¶ [l] g â†” g =á¶ [l] f := âŸ¨.symm, .symmâŸ©

@[trans]
theorem EventuallyEq.trans {l : Filter Î±} {f g h : Î± â†’ Î²} (Hâ‚ : f =á¶ [l] g) (Hâ‚‚ : g =á¶ [l] h) :
    f =á¶ [l] h :=
  Hâ‚‚.rw (fun x y => f x = y) Hâ‚

theorem EventuallyEq.congr_left {l : Filter Î±} {f g h : Î± â†’ Î²} (H : f =á¶ [l] g) :
    f =á¶ [l] h â†” g =á¶ [l] h :=
  âŸ¨H.symm.trans, H.transâŸ©

theorem EventuallyEq.congr_right {l : Filter Î±} {f g h : Î± â†’ Î²} (H : g =á¶ [l] h) :
    f =á¶ [l] g â†” f =á¶ [l] h :=
  âŸ¨(Â·.trans H), (Â·.trans H.symm)âŸ©

instance {l : Filter Î±} :
    Trans ((Â· =á¶ [l] Â·) : (Î± â†’ Î²) â†’ (Î± â†’ Î²) â†’ Prop) (Â· =á¶ [l] Â·) (Â· =á¶ [l] Â·) where
  trans := EventuallyEq.trans

theorem EventuallyEq.prod_mk {l} {f f' : Î± â†’ Î²} (hf : f =á¶ [l] f') {g g' : Î± â†’ Î³} (hg : g =á¶ [l] g') :
    (fun x => (f x, g x)) =á¶ [l] fun x => (f' x, g' x) :=
  hf.mp <|
    hg.mono <| by
      intros
      simp only [*]

-- See `EventuallyEq.comp_tendsto` further below for a similar statement w.r.t.
-- composition on the right.
theorem EventuallyEq.fun_comp {f g : Î± â†’ Î²} {l : Filter Î±} (H : f =á¶ [l] g) (h : Î² â†’ Î³) :
    h âˆ˜ f =á¶ [l] h âˆ˜ g :=
  H.mono fun _ hx => congr_arg h hx

theorem EventuallyEq.compâ‚‚ {Î´} {f f' : Î± â†’ Î²} {g g' : Î± â†’ Î³} {l} (Hf : f =á¶ [l] f') (h : Î² â†’ Î³ â†’ Î´)
    (Hg : g =á¶ [l] g') : (fun x => h (f x) (g x)) =á¶ [l] fun x => h (f' x) (g' x) :=
  (Hf.prod_mk Hg).fun_comp (uncurry h)

@[to_additive]
theorem EventuallyEq.mul [Mul Î²] {f f' g g' : Î± â†’ Î²} {l : Filter Î±} (h : f =á¶ [l] g)
    (h' : f' =á¶ [l] g') : (fun x => f x * f' x) =á¶ [l] fun x => g x * g' x :=
  h.compâ‚‚ (Â· * Â·) h'

@[to_additive const_smul]
theorem EventuallyEq.pow_const {Î³} [Pow Î² Î³] {f g : Î± â†’ Î²} {l : Filter Î±} (h : f =á¶ [l] g) (c : Î³) :
    (fun x => f x ^ c) =á¶ [l] fun x => g x ^ c :=
  h.fun_comp (Â· ^ c)

@[to_additive]
theorem EventuallyEq.inv [Inv Î²] {f g : Î± â†’ Î²} {l : Filter Î±} (h : f =á¶ [l] g) :
    (fun x => (f x)â»Â¹) =á¶ [l] fun x => (g x)â»Â¹ :=
  h.fun_comp Inv.inv

@[to_additive]
theorem EventuallyEq.div [Div Î²] {f f' g g' : Î± â†’ Î²} {l : Filter Î±} (h : f =á¶ [l] g)
    (h' : f' =á¶ [l] g') : (fun x => f x / f' x) =á¶ [l] fun x => g x / g' x :=
  h.compâ‚‚ (Â· / Â·) h'

attribute [to_additive] EventuallyEq.const_smul

@[to_additive]
theorem EventuallyEq.smul {ğ•œ} [SMul ğ•œ Î²] {l : Filter Î±} {f f' : Î± â†’ ğ•œ} {g g' : Î± â†’ Î²}
    (hf : f =á¶ [l] f') (hg : g =á¶ [l] g') : (fun x => f x â€¢ g x) =á¶ [l] fun x => f' x â€¢ g' x :=
  hf.compâ‚‚ (Â· â€¢ Â·) hg

theorem EventuallyEq.sup [Sup Î²] {l : Filter Î±} {f f' g g' : Î± â†’ Î²} (hf : f =á¶ [l] f')
    (hg : g =á¶ [l] g') : (fun x => f x âŠ” g x) =á¶ [l] fun x => f' x âŠ” g' x :=
  hf.compâ‚‚ (Â· âŠ” Â·) hg

theorem EventuallyEq.inf [Inf Î²] {l : Filter Î±} {f f' g g' : Î± â†’ Î²} (hf : f =á¶ [l] f')
    (hg : g =á¶ [l] g') : (fun x => f x âŠ“ g x) =á¶ [l] fun x => f' x âŠ“ g' x :=
  hf.compâ‚‚ (Â· âŠ“ Â·) hg

theorem EventuallyEq.preimage {l : Filter Î±} {f g : Î± â†’ Î²} (h : f =á¶ [l] g) (s : Set Î²) :
    f â»Â¹' s =á¶ [l] g â»Â¹' s :=
  h.fun_comp s

theorem EventuallyEq.inter {s t s' t' : Set Î±} {l : Filter Î±} (h : s =á¶ [l] t) (h' : s' =á¶ [l] t') :
    (s âˆ© s' : Set Î±) =á¶ [l] (t âˆ© t' : Set Î±) :=
  h.compâ‚‚ (Â· âˆ§ Â·) h'

theorem EventuallyEq.union {s t s' t' : Set Î±} {l : Filter Î±} (h : s =á¶ [l] t) (h' : s' =á¶ [l] t') :
    (s âˆª s' : Set Î±) =á¶ [l] (t âˆª t' : Set Î±) :=
  h.compâ‚‚ (Â· âˆ¨ Â·) h'

theorem EventuallyEq.compl {s t : Set Î±} {l : Filter Î±} (h : s =á¶ [l] t) :
    (sá¶œ : Set Î±) =á¶ [l] (tá¶œ : Set Î±) :=
  h.fun_comp Not

theorem EventuallyEq.diff {s t s' t' : Set Î±} {l : Filter Î±} (h : s =á¶ [l] t) (h' : s' =á¶ [l] t') :
    (s \ s' : Set Î±) =á¶ [l] (t \ t' : Set Î±) :=
  h.inter h'.compl

protected theorem EventuallyEq.symmDiff {s t s' t' : Set Î±} {l : Filter Î±}
    (h : s =á¶ [l] t) (h' : s' =á¶ [l] t') : (s âˆ† s' : Set Î±) =á¶ [l] (t âˆ† t' : Set Î±) :=
  (h.diff h').union (h'.diff h)

theorem eventuallyEq_empty {s : Set Î±} {l : Filter Î±} : s =á¶ [l] (âˆ… : Set Î±) â†” âˆ€á¶  x in l, x âˆ‰ s :=
  eventuallyEq_set.trans <| by simp

theorem inter_eventuallyEq_left {s t : Set Î±} {l : Filter Î±} :
    (s âˆ© t : Set Î±) =á¶ [l] s â†” âˆ€á¶  x in l, x âˆˆ s â†’ x âˆˆ t := by
  simp only [eventuallyEq_set, mem_inter_iff, and_iff_left_iff_imp]

theorem inter_eventuallyEq_right {s t : Set Î±} {l : Filter Î±} :
    (s âˆ© t : Set Î±) =á¶ [l] t â†” âˆ€á¶  x in l, x âˆˆ t â†’ x âˆˆ s := by
  rw [inter_comm, inter_eventuallyEq_left]

@[simp]
theorem eventuallyEq_principal {s : Set Î±} {f g : Î± â†’ Î²} : f =á¶ [ğ“Ÿ s] g â†” EqOn f g s :=
  Iff.rfl

theorem eventuallyEq_inf_principal_iff {F : Filter Î±} {s : Set Î±} {f g : Î± â†’ Î²} :
    f =á¶ [F âŠ“ ğ“Ÿ s] g â†” âˆ€á¶  x in F, x âˆˆ s â†’ f x = g x :=
  eventually_inf_principal

theorem EventuallyEq.sub_eq [AddGroup Î²] {f g : Î± â†’ Î²} {l : Filter Î±} (h : f =á¶ [l] g) :
    f - g =á¶ [l] 0 := by simpa using ((EventuallyEq.refl l f).sub h).symm

theorem eventuallyEq_iff_sub [AddGroup Î²] {f g : Î± â†’ Î²} {l : Filter Î±} :
    f =á¶ [l] g â†” f - g =á¶ [l] 0 :=
  âŸ¨fun h => h.sub_eq, fun h => by simpa using h.add (EventuallyEq.refl l g)âŸ©

section LE

variable [LE Î²] {l : Filter Î±}

/-- A function `f` is eventually less than or equal to a function `g` at a filter `l`. -/
def EventuallyLE (l : Filter Î±) (f g : Î± â†’ Î²) : Prop :=
  âˆ€á¶  x in l, f x â‰¤ g x

@[inherit_doc]
notation:50 f " â‰¤á¶ [" l:50 "] " g:50 => EventuallyLE l f g

theorem EventuallyLE.congr {f f' g g' : Î± â†’ Î²} (H : f â‰¤á¶ [l] g) (hf : f =á¶ [l] f') (hg : g =á¶ [l] g') :
    f' â‰¤á¶ [l] g' :=
  H.mp <| hg.mp <| hf.mono fun x hf hg H => by rwa [hf, hg] at H

theorem eventuallyLE_congr {f f' g g' : Î± â†’ Î²} (hf : f =á¶ [l] f') (hg : g =á¶ [l] g') :
    f â‰¤á¶ [l] g â†” f' â‰¤á¶ [l] g' :=
  âŸ¨fun H => H.congr hf hg, fun H => H.congr hf.symm hg.symmâŸ©

end LE

section Preorder

variable [Preorder Î²] {l : Filter Î±} {f g h : Î± â†’ Î²}

theorem EventuallyEq.le (h : f =á¶ [l] g) : f â‰¤á¶ [l] g :=
  h.mono fun _ => le_of_eq

@[refl]
theorem EventuallyLE.refl (l : Filter Î±) (f : Î± â†’ Î²) : f â‰¤á¶ [l] f :=
  EventuallyEq.rfl.le

theorem EventuallyLE.rfl : f â‰¤á¶ [l] f :=
  EventuallyLE.refl l f

@[trans]
theorem EventuallyLE.trans (Hâ‚ : f â‰¤á¶ [l] g) (Hâ‚‚ : g â‰¤á¶ [l] h) : f â‰¤á¶ [l] h :=
  Hâ‚‚.mp <| Hâ‚.mono fun _ => le_trans

instance : Trans ((Â· â‰¤á¶ [l] Â·) : (Î± â†’ Î²) â†’ (Î± â†’ Î²) â†’ Prop) (Â· â‰¤á¶ [l] Â·) (Â· â‰¤á¶ [l] Â·) where
  trans := EventuallyLE.trans

@[trans]
theorem EventuallyEq.trans_le (Hâ‚ : f =á¶ [l] g) (Hâ‚‚ : g â‰¤á¶ [l] h) : f â‰¤á¶ [l] h :=
  Hâ‚.le.trans Hâ‚‚

instance : Trans ((Â· =á¶ [l] Â·) : (Î± â†’ Î²) â†’ (Î± â†’ Î²) â†’ Prop) (Â· â‰¤á¶ [l] Â·) (Â· â‰¤á¶ [l] Â·) where
  trans := EventuallyEq.trans_le

@[trans]
theorem EventuallyLE.trans_eq (Hâ‚ : f â‰¤á¶ [l] g) (Hâ‚‚ : g =á¶ [l] h) : f â‰¤á¶ [l] h :=
  Hâ‚.trans Hâ‚‚.le

instance : Trans ((Â· â‰¤á¶ [l] Â·) : (Î± â†’ Î²) â†’ (Î± â†’ Î²) â†’ Prop) (Â· =á¶ [l] Â·) (Â· â‰¤á¶ [l] Â·) where
  trans := EventuallyLE.trans_eq

end Preorder

variable {l : Filter Î±}

theorem EventuallyLE.antisymm [PartialOrder Î²] {l : Filter Î±} {f g : Î± â†’ Î²} (hâ‚ : f â‰¤á¶ [l] g)
    (hâ‚‚ : g â‰¤á¶ [l] f) : f =á¶ [l] g :=
  hâ‚‚.mp <| hâ‚.mono fun _ => le_antisymm

theorem eventuallyLE_antisymm_iff [PartialOrder Î²] {l : Filter Î±} {f g : Î± â†’ Î²} :
    f =á¶ [l] g â†” f â‰¤á¶ [l] g âˆ§ g â‰¤á¶ [l] f := by
  simp only [EventuallyEq, EventuallyLE, le_antisymm_iff, eventually_and]

theorem EventuallyLE.le_iff_eq [PartialOrder Î²] {l : Filter Î±} {f g : Î± â†’ Î²} (h : f â‰¤á¶ [l] g) :
    g â‰¤á¶ [l] f â†” g =á¶ [l] f :=
  âŸ¨fun h' => h'.antisymm h, EventuallyEq.leâŸ©

theorem Eventually.ne_of_lt [Preorder Î²] {l : Filter Î±} {f g : Î± â†’ Î²} (h : âˆ€á¶  x in l, f x < g x) :
    âˆ€á¶  x in l, f x â‰  g x :=
  h.mono fun _ hx => hx.ne

theorem Eventually.ne_top_of_lt [PartialOrder Î²] [OrderTop Î²] {l : Filter Î±} {f g : Î± â†’ Î²}
    (h : âˆ€á¶  x in l, f x < g x) : âˆ€á¶  x in l, f x â‰  âŠ¤ :=
  h.mono fun _ hx => hx.ne_top

theorem Eventually.lt_top_of_ne [PartialOrder Î²] [OrderTop Î²] {l : Filter Î±} {f : Î± â†’ Î²}
    (h : âˆ€á¶  x in l, f x â‰  âŠ¤) : âˆ€á¶  x in l, f x < âŠ¤ :=
  h.mono fun _ hx => hx.lt_top

theorem Eventually.lt_top_iff_ne_top [PartialOrder Î²] [OrderTop Î²] {l : Filter Î±} {f : Î± â†’ Î²} :
    (âˆ€á¶  x in l, f x < âŠ¤) â†” âˆ€á¶  x in l, f x â‰  âŠ¤ :=
  âŸ¨Eventually.ne_of_lt, Eventually.lt_top_of_neâŸ©

@[mono]
theorem EventuallyLE.inter {s t s' t' : Set Î±} {l : Filter Î±} (h : s â‰¤á¶ [l] t) (h' : s' â‰¤á¶ [l] t') :
    (s âˆ© s' : Set Î±) â‰¤á¶ [l] (t âˆ© t' : Set Î±) :=
  h'.mp <| h.mono fun _ => And.imp

@[mono]
theorem EventuallyLE.union {s t s' t' : Set Î±} {l : Filter Î±} (h : s â‰¤á¶ [l] t) (h' : s' â‰¤á¶ [l] t') :
    (s âˆª s' : Set Î±) â‰¤á¶ [l] (t âˆª t' : Set Î±) :=
  h'.mp <| h.mono fun _ => Or.imp

protected lemma EventuallyLE.iUnion [Finite Î¹] {s t : Î¹ â†’ Set Î±}
    (h : âˆ€ i, s i â‰¤á¶ [l] t i) : (â‹ƒ i, s i) â‰¤á¶ [l] â‹ƒ i, t i :=
  (eventually_all.2 h).mono fun _x hx hx' â†¦
    let âŸ¨i, hiâŸ© := mem_iUnion.1 hx'; mem_iUnion.2 âŸ¨i, hx i hiâŸ©

protected lemma EventuallyEq.iUnion [Finite Î¹] {s t : Î¹ â†’ Set Î±}
    (h : âˆ€ i, s i =á¶ [l] t i) : (â‹ƒ i, s i) =á¶ [l] â‹ƒ i, t i :=
  (EventuallyLE.iUnion fun i â†¦ (h i).le).antisymm <| .iUnion fun i â†¦ (h i).symm.le

protected lemma EventuallyLE.iInter [Finite Î¹] {s t : Î¹ â†’ Set Î±}
    (h : âˆ€ i, s i â‰¤á¶ [l] t i) : (â‹‚ i, s i) â‰¤á¶ [l] â‹‚ i, t i :=
  (eventually_all.2 h).mono fun _x hx hx' â†¦ mem_iInter.2 fun i â†¦ hx i (mem_iInter.1 hx' i)

protected lemma EventuallyEq.iInter [Finite Î¹] {s t : Î¹ â†’ Set Î±}
    (h : âˆ€ i, s i =á¶ [l] t i) : (â‹‚ i, s i) =á¶ [l] â‹‚ i, t i :=
  (EventuallyLE.iInter fun i â†¦ (h i).le).antisymm <| .iInter fun i â†¦ (h i).symm.le

lemma _root_.Set.Finite.eventuallyLE_iUnion {Î¹ : Type*} {s : Set Î¹} (hs : s.Finite)
    {f g : Î¹ â†’ Set Î±} (hle : âˆ€ i âˆˆ s, f i â‰¤á¶ [l] g i) : (â‹ƒ i âˆˆ s, f i) â‰¤á¶ [l] (â‹ƒ i âˆˆ s, g i) := by
  have := hs.to_subtype
  rw [biUnion_eq_iUnion, biUnion_eq_iUnion]
  exact .iUnion fun i â†¦ hle i.1 i.2

alias EventuallyLE.biUnion := Set.Finite.eventuallyLE_iUnion

lemma _root_.Set.Finite.eventuallyEq_iUnion {Î¹ : Type*} {s : Set Î¹} (hs : s.Finite)
    {f g : Î¹ â†’ Set Î±} (heq : âˆ€ i âˆˆ s, f i =á¶ [l] g i) : (â‹ƒ i âˆˆ s, f i) =á¶ [l] (â‹ƒ i âˆˆ s, g i) :=
  (EventuallyLE.biUnion hs fun i hi â†¦ (heq i hi).le).antisymm <|
    .biUnion hs fun i hi â†¦ (heq i hi).symm.le

alias EventuallyEq.biUnion := Set.Finite.eventuallyEq_iUnion

lemma _root_.Set.Finite.eventuallyLE_iInter {Î¹ : Type*} {s : Set Î¹} (hs : s.Finite)
    {f g : Î¹ â†’ Set Î±} (hle : âˆ€ i âˆˆ s, f i â‰¤á¶ [l] g i) : (â‹‚ i âˆˆ s, f i) â‰¤á¶ [l] (â‹‚ i âˆˆ s, g i) := by
  have := hs.to_subtype
  rw [biInter_eq_iInter, biInter_eq_iInter]
  exact .iInter fun i â†¦ hle i.1 i.2

alias EventuallyLE.biInter := Set.Finite.eventuallyLE_iInter

lemma _root_.Set.Finite.eventuallyEq_iInter {Î¹ : Type*} {s : Set Î¹} (hs : s.Finite)
    {f g : Î¹ â†’ Set Î±} (heq : âˆ€ i âˆˆ s, f i =á¶ [l] g i) : (â‹‚ i âˆˆ s, f i) =á¶ [l] (â‹‚ i âˆˆ s, g i) :=
  (EventuallyLE.biInter hs fun i hi â†¦ (heq i hi).le).antisymm <|
    .biInter hs fun i hi â†¦ (heq i hi).symm.le

alias EventuallyEq.biInter := Set.Finite.eventuallyEq_iInter

lemma _root_.Finset.eventuallyLE_iUnion {Î¹ : Type*} (s : Finset Î¹) {f g : Î¹ â†’ Set Î±}
    (hle : âˆ€ i âˆˆ s, f i â‰¤á¶ [l] g i) : (â‹ƒ i âˆˆ s, f i) â‰¤á¶ [l] (â‹ƒ i âˆˆ s, g i) :=
  .biUnion s.finite_toSet hle

lemma _root_.Finset.eventuallyEq_iUnion {Î¹ : Type*} (s : Finset Î¹) {f g : Î¹ â†’ Set Î±}
    (heq : âˆ€ i âˆˆ s, f i =á¶ [l] g i) : (â‹ƒ i âˆˆ s, f i) =á¶ [l] (â‹ƒ i âˆˆ s, g i) :=
  .biUnion s.finite_toSet heq

lemma _root_.Finset.eventuallyLE_iInter {Î¹ : Type*} (s : Finset Î¹) {f g : Î¹ â†’ Set Î±}
    (hle : âˆ€ i âˆˆ s, f i â‰¤á¶ [l] g i) : (â‹‚ i âˆˆ s, f i) â‰¤á¶ [l] (â‹‚ i âˆˆ s, g i) :=
  .biInter s.finite_toSet hle

lemma _root_.Finset.eventuallyEq_iInter {Î¹ : Type*} (s : Finset Î¹) {f g : Î¹ â†’ Set Î±}
    (heq : âˆ€ i âˆˆ s, f i =á¶ [l] g i) : (â‹‚ i âˆˆ s, f i) =á¶ [l] (â‹‚ i âˆˆ s, g i) :=
  .biInter s.finite_toSet heq

@[mono]
theorem EventuallyLE.compl {s t : Set Î±} {l : Filter Î±} (h : s â‰¤á¶ [l] t) :
    (tá¶œ : Set Î±) â‰¤á¶ [l] (sá¶œ : Set Î±) :=
  h.mono fun _ => mt

@[mono]
theorem EventuallyLE.diff {s t s' t' : Set Î±} {l : Filter Î±} (h : s â‰¤á¶ [l] t) (h' : t' â‰¤á¶ [l] s') :
    (s \ s' : Set Î±) â‰¤á¶ [l] (t \ t' : Set Î±) :=
  h.inter h'.compl

theorem set_eventuallyLE_iff_mem_inf_principal {s t : Set Î±} {l : Filter Î±} :
    s â‰¤á¶ [l] t â†” t âˆˆ l âŠ“ ğ“Ÿ s :=
  eventually_inf_principal.symm

theorem set_eventuallyLE_iff_inf_principal_le {s t : Set Î±} {l : Filter Î±} :
    s â‰¤á¶ [l] t â†” l âŠ“ ğ“Ÿ s â‰¤ l âŠ“ ğ“Ÿ t :=
  set_eventuallyLE_iff_mem_inf_principal.trans <| by
    simp only [le_inf_iff, inf_le_left, true_and_iff, le_principal_iff]

theorem set_eventuallyEq_iff_inf_principal {s t : Set Î±} {l : Filter Î±} :
    s =á¶ [l] t â†” l âŠ“ ğ“Ÿ s = l âŠ“ ğ“Ÿ t := by
  simp only [eventuallyLE_antisymm_iff, le_antisymm_iff, set_eventuallyLE_iff_inf_principal_le]

theorem EventuallyLE.sup [SemilatticeSup Î²] {l : Filter Î±} {fâ‚ fâ‚‚ gâ‚ gâ‚‚ : Î± â†’ Î²} (hf : fâ‚ â‰¤á¶ [l] fâ‚‚)
    (hg : gâ‚ â‰¤á¶ [l] gâ‚‚) : fâ‚ âŠ” gâ‚ â‰¤á¶ [l] fâ‚‚ âŠ” gâ‚‚ := by
  filter_upwards [hf, hg] with x hfx hgx using sup_le_sup hfx hgx

theorem EventuallyLE.sup_le [SemilatticeSup Î²] {l : Filter Î±} {f g h : Î± â†’ Î²} (hf : f â‰¤á¶ [l] h)
    (hg : g â‰¤á¶ [l] h) : f âŠ” g â‰¤á¶ [l] h := by
  filter_upwards [hf, hg] with x hfx hgx using _root_.sup_le hfx hgx

theorem EventuallyLE.le_sup_of_le_left [SemilatticeSup Î²] {l : Filter Î±} {f g h : Î± â†’ Î²}
    (hf : h â‰¤á¶ [l] f) : h â‰¤á¶ [l] f âŠ” g :=
  hf.mono fun _ => _root_.le_sup_of_le_left

theorem EventuallyLE.le_sup_of_le_right [SemilatticeSup Î²] {l : Filter Î±} {f g h : Î± â†’ Î²}
    (hg : h â‰¤á¶ [l] g) : h â‰¤á¶ [l] f âŠ” g :=
  hg.mono fun _ => _root_.le_sup_of_le_right

theorem join_le {f : Filter (Filter Î±)} {l : Filter Î±} (h : âˆ€á¶  m in f, m â‰¤ l) : join f â‰¤ l :=
  fun _ hs => h.mono fun _ hm => hm hs

/-! ### Push-forwards, pull-backs, and the monad structure -/

section Map

/-- The forward map of a filter -/
def map (m : Î± â†’ Î²) (f : Filter Î±) : Filter Î² where
  sets := preimage m â»Â¹' f.sets
  univ_sets := univ_mem
  sets_of_superset hs st := mem_of_superset hs <| preimage_mono st
  inter_sets hs ht := inter_mem hs ht

@[simp]
theorem map_principal {s : Set Î±} {f : Î± â†’ Î²} : map f (ğ“Ÿ s) = ğ“Ÿ (Set.image f s) :=
  Filter.ext fun _ => image_subset_iff.symm

variable {f : Filter Î±} {m : Î± â†’ Î²} {m' : Î² â†’ Î³} {s : Set Î±} {t : Set Î²}

@[simp]
theorem eventually_map {P : Î² â†’ Prop} : (âˆ€á¶  b in map m f, P b) â†” âˆ€á¶  a in f, P (m a) :=
  Iff.rfl

@[simp]
theorem frequently_map {P : Î² â†’ Prop} : (âˆƒá¶  b in map m f, P b) â†” âˆƒá¶  a in f, P (m a) :=
  Iff.rfl

@[simp]
theorem mem_map : t âˆˆ map m f â†” m â»Â¹' t âˆˆ f :=
  Iff.rfl

theorem mem_map' : t âˆˆ map m f â†” { x | m x âˆˆ t } âˆˆ f :=
  Iff.rfl

theorem image_mem_map (hs : s âˆˆ f) : m '' s âˆˆ map m f :=
  f.sets_of_superset hs <| subset_preimage_image m s

-- The simpNF linter says that the LHS can be simplified via `Filter.mem_map`.
-- However this is a higher priority lemma.
-- https://github.com/leanprover/std4/issues/207
@[simp 1100, nolint simpNF]
theorem image_mem_map_iff (hf : Injective m) : m '' s âˆˆ map m f â†” s âˆˆ f :=
  âŸ¨fun h => by rwa [â† preimage_image_eq s hf], image_mem_mapâŸ©

theorem range_mem_map : range m âˆˆ map m f := by
  rw [â† image_univ]
  exact image_mem_map univ_mem

theorem mem_map_iff_exists_image : t âˆˆ map m f â†” âˆƒ s âˆˆ f, m '' s âŠ† t :=
  âŸ¨fun ht => âŸ¨m â»Â¹' t, ht, image_preimage_subset _ _âŸ©, fun âŸ¨_, hs, htâŸ© =>
    mem_of_superset (image_mem_map hs) htâŸ©

@[simp]
theorem map_id : Filter.map id f = f :=
  filter_eq <| rfl

@[simp]
theorem map_id' : Filter.map (fun x => x) f = f :=
  map_id

@[simp]
theorem map_compose : Filter.map m' âˆ˜ Filter.map m = Filter.map (m' âˆ˜ m) :=
  funext fun _ => filter_eq <| rfl

@[simp]
theorem map_map : Filter.map m' (Filter.map m f) = Filter.map (m' âˆ˜ m) f :=
  congr_fun Filter.map_compose f

/-- If functions `mâ‚` and `mâ‚‚` are eventually equal at a filter `f`, then
they map this filter to the same filter. -/
theorem map_congr {mâ‚ mâ‚‚ : Î± â†’ Î²} {f : Filter Î±} (h : mâ‚ =á¶ [f] mâ‚‚) : map mâ‚ f = map mâ‚‚ f :=
  Filter.ext' fun _ => eventually_congr (h.mono fun _ hx => hx â–¸ Iff.rfl)

end Map

section Comap

/-- The inverse map of a filter. A set `s` belongs to `Filter.comap m f` if either of the following
equivalent conditions hold.

1. There exists a set `t âˆˆ f` such that `m â»Â¹' t âŠ† s`. This is used as a definition.
2. The set `kernImage m s = {y | âˆ€ x, m x = y â†’ x âˆˆ s}` belongs to `f`, see `Filter.mem_comap'`.
3. The set `(m '' sá¶œ)á¶œ` belongs to `f`, see `Filter.mem_comap_iff_compl` and
`Filter.compl_mem_comap`. -/
def comap (m : Î± â†’ Î²) (f : Filter Î²) : Filter Î± where
  sets := { s | âˆƒ t âˆˆ f, m â»Â¹' t âŠ† s }
  univ_sets := âŸ¨univ, univ_mem, by simp only [subset_univ, preimage_univ]âŸ©
  sets_of_superset := fun âŸ¨a', ha', ma'aâŸ© ab => âŸ¨a', ha', ma'a.trans abâŸ©
  inter_sets := fun âŸ¨a', haâ‚, haâ‚‚âŸ© âŸ¨b', hbâ‚, hbâ‚‚âŸ© =>
    âŸ¨a' âˆ© b', inter_mem haâ‚ hbâ‚, inter_subset_inter haâ‚‚ hbâ‚‚âŸ©

variable {f : Î± â†’ Î²} {l : Filter Î²} {p : Î± â†’ Prop} {s : Set Î±}

theorem mem_comap' : s âˆˆ comap f l â†” { y | âˆ€ â¦ƒxâ¦„, f x = y â†’ x âˆˆ s } âˆˆ l :=
  âŸ¨fun âŸ¨t, ht, htsâŸ© => mem_of_superset ht fun y hy x hx => hts <| mem_preimage.2 <| by rwa [hx],
    fun h => âŸ¨_, h, fun x hx => hx rflâŸ©âŸ©

-- TODO: it would be nice to use `kernImage` much more to take advantage of common name and API,
-- and then this would become `mem_comap'`
theorem mem_comap'' : s âˆˆ comap f l â†” kernImage f s âˆˆ l :=
  mem_comap'

/-- RHS form is used, e.g., in the definition of `UniformSpace`. -/
lemma mem_comap_prod_mk {x : Î±} {s : Set Î²} {F : Filter (Î± Ã— Î²)} :
    s âˆˆ comap (Prod.mk x) F â†” {p : Î± Ã— Î² | p.fst = x â†’ p.snd âˆˆ s} âˆˆ F := by
  simp_rw [mem_comap', Prod.ext_iff, and_imp, @forall_swap Î² (_ = _), forall_eq, eq_comm]

@[simp]
theorem eventually_comap : (âˆ€á¶  a in comap f l, p a) â†” âˆ€á¶  b in l, âˆ€ a, f a = b â†’ p a :=
  mem_comap'

@[simp]
theorem frequently_comap : (âˆƒá¶  a in comap f l, p a) â†” âˆƒá¶  b in l, âˆƒ a, f a = b âˆ§ p a := by
  simp only [Filter.Frequently, eventually_comap, not_exists, _root_.not_and]

theorem mem_comap_iff_compl : s âˆˆ comap f l â†” (f '' sá¶œ)á¶œ âˆˆ l := by
  simp only [mem_comap'', kernImage_eq_compl]

theorem compl_mem_comap : sá¶œ âˆˆ comap f l â†” (f '' s)á¶œ âˆˆ l := by rw [mem_comap_iff_compl, compl_compl]

end Comap

section KernMap

/-- The analog of `kernImage` for filters. A set `s` belongs to `Filter.kernMap m f` if either of
the following equivalent conditions hold.

1. There exists a set `t âˆˆ f` such that `s = kernImage m t`. This is used as a definition.
2. There exists a set `t` such that `tá¶œ âˆˆ f` and `sá¶œ = m '' t`, see `Filter.mem_kernMap_iff_compl`
and `Filter.compl_mem_kernMap`.

This definition because it gives a right adjoint to `Filter.comap`, and because it has a nice
interpretation when working with `co-` filters (`Filter.cocompact`, `Filter.cofinite`, ...).
For example, `kernMap m (cocompact Î±)` is the filter generated by the complements of the sets
`m '' K` where `K` is a compact subset of `Î±`. -/
def kernMap (m : Î± â†’ Î²) (f : Filter Î±) : Filter Î² where
  sets := (kernImage m) '' f.sets
  univ_sets := âŸ¨univ, f.univ_sets, by simp [kernImage_eq_compl]âŸ©
  sets_of_superset := by
    rintro _ t âŸ¨s, hs, rflâŸ© hst
    refine âŸ¨s âˆª m â»Â¹' t, mem_of_superset hs subset_union_left, ?_âŸ©
    rw [kernImage_union_preimage, union_eq_right.mpr hst]
  inter_sets := by
    rintro _ _ âŸ¨sâ‚, hâ‚, rflâŸ© âŸ¨sâ‚‚, hâ‚‚, rflâŸ©
    exact âŸ¨sâ‚ âˆ© sâ‚‚, f.inter_sets hâ‚ hâ‚‚, Set.preimage_kernImage.u_infâŸ©

variable {m : Î± â†’ Î²} {f : Filter Î±}

theorem mem_kernMap {s : Set Î²} : s âˆˆ kernMap m f â†” âˆƒ t âˆˆ f, kernImage m t = s :=
  Iff.rfl

theorem mem_kernMap_iff_compl {s : Set Î²} : s âˆˆ kernMap m f â†” âˆƒ t, tá¶œ âˆˆ f âˆ§ m '' t = sá¶œ := by
  rw [mem_kernMap, compl_surjective.exists]
  refine exists_congr (fun x â†¦ and_congr_right fun _ â†¦ ?_)
  rw [kernImage_compl, compl_eq_comm, eq_comm]

theorem compl_mem_kernMap {s : Set Î²} : sá¶œ âˆˆ kernMap m f â†” âˆƒ t, tá¶œ âˆˆ f âˆ§ m '' t = s := by
  simp_rw [mem_kernMap_iff_compl, compl_compl]

end KernMap

/-- The monadic bind operation on filter is defined the usual way in terms of `map` and `join`.

Unfortunately, this `bind` does not result in the expected applicative. See `Filter.seq` for the
applicative instance. -/
def bind (f : Filter Î±) (m : Î± â†’ Filter Î²) : Filter Î² :=
  join (map m f)

/-- The applicative sequentiation operation. This is not induced by the bind operation. -/
def seq (f : Filter (Î± â†’ Î²)) (g : Filter Î±) : Filter Î² where
  sets := { s | âˆƒ u âˆˆ f, âˆƒ t âˆˆ g, âˆ€ m âˆˆ u, âˆ€ x âˆˆ t, (m : Î± â†’ Î²) x âˆˆ s }
  univ_sets := âŸ¨univ, univ_mem, univ, univ_mem, fun _ _ _ _ => trivialâŸ©
  sets_of_superset := fun âŸ¨tâ‚€, tâ‚, hâ‚€, hâ‚, hâŸ© hst =>
    âŸ¨tâ‚€, tâ‚, hâ‚€, hâ‚, fun _ hx _ hy => hst <| h _ hx _ hyâŸ©
  inter_sets := fun âŸ¨tâ‚€, htâ‚€, tâ‚, htâ‚, htâŸ© âŸ¨uâ‚€, huâ‚€, uâ‚, huâ‚, huâŸ© =>
    âŸ¨tâ‚€ âˆ© uâ‚€, inter_mem htâ‚€ huâ‚€, tâ‚ âˆ© uâ‚, inter_mem htâ‚ huâ‚, fun _ âŸ¨hxâ‚€, hxâ‚âŸ© _ âŸ¨hyâ‚€, hyâ‚âŸ© =>
      âŸ¨ht _ hxâ‚€ _ hyâ‚€, hu _ hxâ‚ _ hyâ‚âŸ©âŸ©

/-- `pure x` is the set of sets that contain `x`. It is equal to `ğ“Ÿ {x}` but
with this definition we have `s âˆˆ pure a` defeq `a âˆˆ s`. -/
instance : Pure Filter :=
  âŸ¨fun x =>
    { sets := { s | x âˆˆ s }
      inter_sets := And.intro
      sets_of_superset := fun hs hst => hst hs
      univ_sets := trivial }âŸ©

instance : Bind Filter :=
  âŸ¨@Filter.bindâŸ©

instance : Functor Filter where map := @Filter.map

instance : LawfulFunctor (Filter : Type u â†’ Type u) where
  id_map _ := map_id
  comp_map _ _ _ := map_map.symm
  map_const := rfl

theorem pure_sets (a : Î±) : (pure a : Filter Î±).sets = { s | a âˆˆ s } :=
  rfl

@[simp]
theorem mem_pure {a : Î±} {s : Set Î±} : s âˆˆ (pure a : Filter Î±) â†” a âˆˆ s :=
  Iff.rfl

@[simp]
theorem eventually_pure {a : Î±} {p : Î± â†’ Prop} : (âˆ€á¶  x in pure a, p x) â†” p a :=
  Iff.rfl

@[simp]
theorem principal_singleton (a : Î±) : ğ“Ÿ {a} = pure a :=
  Filter.ext fun s => by simp only [mem_pure, mem_principal, singleton_subset_iff]

@[simp]
theorem map_pure (f : Î± â†’ Î²) (a : Î±) : map f (pure a) = pure (f a) :=
  rfl

theorem pure_le_principal {s : Set Î±} (a : Î±) : pure a â‰¤ ğ“Ÿ s â†” a âˆˆ s := by
  simp

@[simp] theorem join_pure (f : Filter Î±) : join (pure f) = f := rfl

@[simp]
theorem pure_bind (a : Î±) (m : Î± â†’ Filter Î²) : bind (pure a) m = m a := by
  simp only [Bind.bind, bind, map_pure, join_pure]

theorem map_bind {Î± Î²} (m : Î² â†’ Î³) (f : Filter Î±) (g : Î± â†’ Filter Î²) :
    map m (bind f g) = bind f (map m âˆ˜ g) :=
  rfl

theorem bind_map {Î± Î²} (m : Î± â†’ Î²) (f : Filter Î±) (g : Î² â†’ Filter Î³) :
    (bind (map m f) g) = bind f (g âˆ˜ m) :=
  rfl

/-!
### `Filter` as a `Monad`

In this section we define `Filter.monad`, a `Monad` structure on `Filter`s. This definition is not
an instance because its `Seq` projection is not equal to the `Filter.seq` function we use in the
`Applicative` instance on `Filter`.
-/

section

/-- The monad structure on filters. -/
protected def monad : Monad Filter where map := @Filter.map

attribute [local instance] Filter.monad

protected theorem lawfulMonad : LawfulMonad Filter where
  map_const := rfl
  id_map _ := rfl
  seqLeft_eq _ _ := rfl
  seqRight_eq _ _ := rfl
  pure_seq _ _ := rfl
  bind_pure_comp _ _ := rfl
  bind_map _ _ := rfl
  pure_bind _ _ := rfl
  bind_assoc _ _ _ := rfl

end

instance : Alternative Filter where
  seq := fun x y => x.seq (y ())
  failure := âŠ¥
  orElse x y := x âŠ” y ()

@[simp]
theorem map_def {Î± Î²} (m : Î± â†’ Î²) (f : Filter Î±) : m <$> f = map m f :=
  rfl

@[simp]
theorem bind_def {Î± Î²} (f : Filter Î±) (m : Î± â†’ Filter Î²) : f >>= m = bind f m :=
  rfl

/-! #### `map` and `comap` equations -/

section Map

variable {f fâ‚ fâ‚‚ : Filter Î±} {g gâ‚ gâ‚‚ : Filter Î²} {m : Î± â†’ Î²} {m' : Î² â†’ Î³} {s : Set Î±} {t : Set Î²}

@[simp] theorem mem_comap : s âˆˆ comap m g â†” âˆƒ t âˆˆ g, m â»Â¹' t âŠ† s := Iff.rfl

theorem preimage_mem_comap (ht : t âˆˆ g) : m â»Â¹' t âˆˆ comap m g :=
  âŸ¨t, ht, Subset.rflâŸ©

theorem Eventually.comap {p : Î² â†’ Prop} (hf : âˆ€á¶  b in g, p b) (f : Î± â†’ Î²) :
    âˆ€á¶  a in comap f g, p (f a) :=
  preimage_mem_comap hf

theorem comap_id : comap id f = f :=
  le_antisymm (fun _ => preimage_mem_comap) fun _ âŸ¨_, ht, hstâŸ© => mem_of_superset ht hst

theorem comap_id' : comap (fun x => x) f = f := comap_id

theorem comap_const_of_not_mem {x : Î²} (ht : t âˆˆ g) (hx : x âˆ‰ t) : comap (fun _ : Î± => x) g = âŠ¥ :=
  empty_mem_iff_bot.1 <| mem_comap'.2 <| mem_of_superset ht fun _ hx' _ h => hx <| h.symm â–¸ hx'

theorem comap_const_of_mem {x : Î²} (h : âˆ€ t âˆˆ g, x âˆˆ t) : comap (fun _ : Î± => x) g = âŠ¤ :=
  top_unique fun _ hs => univ_mem' fun _ => h _ (mem_comap'.1 hs) rfl

theorem map_const [NeBot f] {c : Î²} : (f.map fun _ => c) = pure c := by
  ext s
  by_cases h : c âˆˆ s <;> simp [h]

theorem comap_comap {m : Î³ â†’ Î²} {n : Î² â†’ Î±} : comap m (comap n f) = comap (n âˆ˜ m) f :=
  Filter.coext fun s => by simp only [compl_mem_comap, image_image, (Â· âˆ˜ Â·)]

section comm

/-!
The variables in the following lemmas are used as in this diagram:
```
    Ï†
  Î± â†’ Î²
Î¸ â†“   â†“ Ïˆ
  Î³ â†’ Î´
    Ï
```
-/


variable {Ï† : Î± â†’ Î²} {Î¸ : Î± â†’ Î³} {Ïˆ : Î² â†’ Î´} {Ï : Î³ â†’ Î´}

theorem map_comm (H : Ïˆ âˆ˜ Ï† = Ï âˆ˜ Î¸) (F : Filter Î±) :
    map Ïˆ (map Ï† F) = map Ï (map Î¸ F) := by
  rw [Filter.map_map, H, â† Filter.map_map]

theorem comap_comm (H : Ïˆ âˆ˜ Ï† = Ï âˆ˜ Î¸) (G : Filter Î´) :
    comap Ï† (comap Ïˆ G) = comap Î¸ (comap Ï G) := by
  rw [Filter.comap_comap, H, â† Filter.comap_comap]

end comm

theorem _root_.Function.Semiconj.filter_map {f : Î± â†’ Î²} {ga : Î± â†’ Î±} {gb : Î² â†’ Î²}
    (h : Function.Semiconj f ga gb) : Function.Semiconj (map f) (map ga) (map gb) :=
  map_comm h.comp_eq

theorem _root_.Function.Commute.filter_map {f g : Î± â†’ Î±} (h : Function.Commute f g) :
    Function.Commute (map f) (map g) :=
  h.semiconj.filter_map

theorem _root_.Function.Semiconj.filter_comap {f : Î± â†’ Î²} {ga : Î± â†’ Î±} {gb : Î² â†’ Î²}
    (h : Function.Semiconj f ga gb) : Function.Semiconj (comap f) (comap gb) (comap ga) :=
  comap_comm h.comp_eq.symm

theorem _root_.Function.Commute.filter_comap {f g : Î± â†’ Î±} (h : Function.Commute f g) :
    Function.Commute (comap f) (comap g) :=
  h.semiconj.filter_comap

section

open Filter

theorem _root_.Function.LeftInverse.filter_map {f : Î± â†’ Î²} {g : Î² â†’ Î±} (hfg : LeftInverse g f) :
    LeftInverse (map g) (map f) := fun F â†¦ by
  rw [map_map, hfg.comp_eq_id, map_id]

theorem _root_.Function.LeftInverse.filter_comap {f : Î± â†’ Î²} {g : Î² â†’ Î±} (hfg : LeftInverse g f) :
    RightInverse (comap g) (comap f) := fun F â†¦ by
  rw [comap_comap, hfg.comp_eq_id, comap_id]

nonrec theorem _root_.Function.RightInverse.filter_map {f : Î± â†’ Î²} {g : Î² â†’ Î±}
    (hfg : RightInverse g f) : RightInverse (map g) (map f) :=
  hfg.filter_map

nonrec theorem _root_.Function.RightInverse.filter_comap {f : Î± â†’ Î²} {g : Î² â†’ Î±}
    (hfg : RightInverse g f) : LeftInverse (comap g) (comap f) :=
  hfg.filter_comap

theorem _root_.Set.LeftInvOn.filter_map_Iic {f : Î± â†’ Î²} {g : Î² â†’ Î±} (hfg : LeftInvOn g f s) :
    LeftInvOn (map g) (map f) (Iic <| ğ“Ÿ s) := fun F (hF : F â‰¤ ğ“Ÿ s) â†¦ by
  have : (g âˆ˜ f) =á¶ [ğ“Ÿ s] id := by simpa only [eventuallyEq_principal] using hfg
  rw [map_map, map_congr (this.filter_mono hF), map_id]

nonrec theorem _root_.Set.RightInvOn.filter_map_Iic {f : Î± â†’ Î²} {g : Î² â†’ Î±}
    (hfg : RightInvOn g f t) : RightInvOn (map g) (map f) (Iic <| ğ“Ÿ t) :=
  hfg.filter_map_Iic

end

@[simp]
theorem comap_principal {t : Set Î²} : comap m (ğ“Ÿ t) = ğ“Ÿ (m â»Â¹' t) :=
  Filter.ext fun _ => âŸ¨fun âŸ¨_u, hu, bâŸ© => (preimage_mono hu).trans b,
    fun h => âŸ¨t, Subset.rfl, hâŸ©âŸ©

theorem principal_subtype {Î± : Type*} (s : Set Î±) (t : Set s) :
    ğ“Ÿ t = comap (â†‘) (ğ“Ÿ (((â†‘) : s â†’ Î±) '' t)) := by
  rw [comap_principal, preimage_image_eq _ Subtype.coe_injective]

@[simp]
theorem comap_pure {b : Î²} : comap m (pure b) = ğ“Ÿ (m â»Â¹' {b}) := by
  rw [â† principal_singleton, comap_principal]

theorem map_le_iff_le_comap : map m f â‰¤ g â†” f â‰¤ comap m g :=
  âŸ¨fun h _ âŸ¨_, ht, htsâŸ© => mem_of_superset (h ht) hts, fun h _ ht => h âŸ¨_, ht, Subset.rflâŸ©âŸ©

theorem gc_map_comap (m : Î± â†’ Î²) : GaloisConnection (map m) (comap m) :=
  fun _ _ => map_le_iff_le_comap

theorem comap_le_iff_le_kernMap : comap m g â‰¤ f â†” g â‰¤ kernMap m f := by
  simp [Filter.le_def, mem_comap'', mem_kernMap, -mem_comap]

theorem gc_comap_kernMap (m : Î± â†’ Î²) : GaloisConnection (comap m) (kernMap m) :=
  fun _ _ â†¦ comap_le_iff_le_kernMap

theorem kernMap_principal {s : Set Î±} : kernMap m (ğ“Ÿ s) = ğ“Ÿ (kernImage m s) := by
  refine eq_of_forall_le_iff (fun g â†¦ ?_)
  rw [â† comap_le_iff_le_kernMap, le_principal_iff, le_principal_iff, mem_comap'']

@[mono]
theorem map_mono : Monotone (map m) :=
  (gc_map_comap m).monotone_l

@[mono]
theorem comap_mono : Monotone (comap m) :=
  (gc_map_comap m).monotone_u

/-- Temporary lemma that we can tag with `gcongr` -/
@[gcongr] theorem _root_.GCongr.Filter.map_le_map {F G : Filter Î±} (h : F â‰¤ G) :
    map m F â‰¤ map m G := map_mono h

/-- Temporary lemma that we can tag with `gcongr` -/
@[gcongr]
theorem _root_.GCongr.Filter.comap_le_comap {F G : Filter Î²} (h : F â‰¤ G) :
    comap m F â‰¤ comap m G := comap_mono h

@[simp] theorem map_bot : map m âŠ¥ = âŠ¥ := (gc_map_comap m).l_bot

@[simp] theorem map_sup : map m (fâ‚ âŠ” fâ‚‚) = map m fâ‚ âŠ” map m fâ‚‚ := (gc_map_comap m).l_sup

@[simp]
theorem map_iSup {f : Î¹ â†’ Filter Î±} : map m (â¨† i, f i) = â¨† i, map m (f i) :=
  (gc_map_comap m).l_iSup

@[simp]
theorem map_top (f : Î± â†’ Î²) : map f âŠ¤ = ğ“Ÿ (range f) := by
  rw [â† principal_univ, map_principal, image_univ]

@[simp] theorem comap_top : comap m âŠ¤ = âŠ¤ := (gc_map_comap m).u_top

@[simp] theorem comap_inf : comap m (gâ‚ âŠ“ gâ‚‚) = comap m gâ‚ âŠ“ comap m gâ‚‚ := (gc_map_comap m).u_inf

@[simp]
theorem comap_iInf {f : Î¹ â†’ Filter Î²} : comap m (â¨… i, f i) = â¨… i, comap m (f i) :=
  (gc_map_comap m).u_iInf

theorem le_comap_top (f : Î± â†’ Î²) (l : Filter Î±) : l â‰¤ comap f âŠ¤ := by
  rw [comap_top]
  exact le_top

theorem map_comap_le : map m (comap m g) â‰¤ g :=
  (gc_map_comap m).l_u_le _

theorem le_comap_map : f â‰¤ comap m (map m f) :=
  (gc_map_comap m).le_u_l _

@[simp]
theorem comap_bot : comap m âŠ¥ = âŠ¥ :=
  bot_unique fun s _ => âŸ¨âˆ…, mem_bot, by simp only [empty_subset, preimage_empty]âŸ©

theorem neBot_of_comap (h : (comap m g).NeBot) : g.NeBot := by
  rw [neBot_iff] at *
  contrapose! h
  rw [h]
  exact comap_bot

theorem comap_inf_principal_range : comap m (g âŠ“ ğ“Ÿ (range m)) = comap m g := by
  simp

theorem disjoint_comap (h : Disjoint gâ‚ gâ‚‚) : Disjoint (comap m gâ‚) (comap m gâ‚‚) := by
  simp only [disjoint_iff, â† comap_inf, h.eq_bot, comap_bot]

theorem comap_iSup {Î¹} {f : Î¹ â†’ Filter Î²} {m : Î± â†’ Î²} : comap m (iSup f) = â¨† i, comap m (f i) :=
  (gc_comap_kernMap m).l_iSup

theorem comap_sSup {s : Set (Filter Î²)} {m : Î± â†’ Î²} : comap m (sSup s) = â¨† f âˆˆ s, comap m f := by
  simp only [sSup_eq_iSup, comap_iSup, eq_self_iff_true]

theorem comap_sup : comap m (gâ‚ âŠ” gâ‚‚) = comap m gâ‚ âŠ” comap m gâ‚‚ := by
  rw [sup_eq_iSup, comap_iSup, iSup_bool_eq, Bool.cond_true, Bool.cond_false]

theorem map_comap (f : Filter Î²) (m : Î± â†’ Î²) : (f.comap m).map m = f âŠ“ ğ“Ÿ (range m) := by
  refine le_antisymm (le_inf map_comap_le <| le_principal_iff.2 range_mem_map) ?_
  rintro t' âŸ¨t, ht, subâŸ©
  refine mem_inf_principal.2 (mem_of_superset ht ?_)
  rintro _ hxt âŸ¨x, rflâŸ©
  exact sub hxt

theorem map_comap_setCoe_val (f : Filter Î²) (s : Set Î²) :
    (f.comap ((â†‘) : s â†’ Î²)).map (â†‘) = f âŠ“ ğ“Ÿ s := by
  rw [map_comap, Subtype.range_val]

theorem map_comap_of_mem {f : Filter Î²} {m : Î± â†’ Î²} (hf : range m âˆˆ f) : (f.comap m).map m = f := by
  rw [map_comap, inf_eq_left.2 (le_principal_iff.2 hf)]

instance canLift (c) (p) [CanLift Î± Î² c p] :
    CanLift (Filter Î±) (Filter Î²) (map c) fun f => âˆ€á¶  x : Î± in f, p x where
  prf f hf := âŸ¨comap c f, map_comap_of_mem <| hf.mono CanLift.prfâŸ©

theorem comap_le_comap_iff {f g : Filter Î²} {m : Î± â†’ Î²} (hf : range m âˆˆ f) :
    comap m f â‰¤ comap m g â†” f â‰¤ g :=
  âŸ¨fun h => map_comap_of_mem hf â–¸ (map_mono h).trans map_comap_le, fun h => comap_mono hâŸ©

theorem map_comap_of_surjective {f : Î± â†’ Î²} (hf : Surjective f) (l : Filter Î²) :
    map f (comap f l) = l :=
  map_comap_of_mem <| by simp only [hf.range_eq, univ_mem]

theorem comap_injective {f : Î± â†’ Î²} (hf : Surjective f) : Injective (comap f) :=
  LeftInverse.injective <| map_comap_of_surjective hf

theorem _root_.Function.Surjective.filter_map_top {f : Î± â†’ Î²} (hf : Surjective f) : map f âŠ¤ = âŠ¤ :=
  (congr_arg _ comap_top).symm.trans <| map_comap_of_surjective hf âŠ¤

theorem subtype_coe_map_comap (s : Set Î±) (f : Filter Î±) :
    map ((â†‘) : s â†’ Î±) (comap ((â†‘) : s â†’ Î±) f) = f âŠ“ ğ“Ÿ s := by rw [map_comap, Subtype.range_coe]

theorem image_mem_of_mem_comap {f : Filter Î±} {c : Î² â†’ Î±} (h : range c âˆˆ f) {W : Set Î²}
    (W_in : W âˆˆ comap c f) : c '' W âˆˆ f := by
  rw [â† map_comap_of_mem h]
  exact image_mem_map W_in

theorem image_coe_mem_of_mem_comap {f : Filter Î±} {U : Set Î±} (h : U âˆˆ f) {W : Set U}
    (W_in : W âˆˆ comap ((â†‘) : U â†’ Î±) f) : (â†‘) '' W âˆˆ f :=
  image_mem_of_mem_comap (by simp [h]) W_in

theorem comap_map {f : Filter Î±} {m : Î± â†’ Î²} (h : Injective m) : comap m (map m f) = f :=
  le_antisymm
    (fun s hs =>
      mem_of_superset (preimage_mem_comap <| image_mem_map hs) <| by
        simp only [preimage_image_eq s h, Subset.rfl])
    le_comap_map

theorem mem_comap_iff {f : Filter Î²} {m : Î± â†’ Î²} (inj : Injective m) (large : Set.range m âˆˆ f)
    {S : Set Î±} : S âˆˆ comap m f â†” m '' S âˆˆ f := by
  rw [â† image_mem_map_iff inj, map_comap_of_mem large]

theorem map_le_map_iff_of_injOn {lâ‚ lâ‚‚ : Filter Î±} {f : Î± â†’ Î²} {s : Set Î±} (hâ‚ : s âˆˆ lâ‚)
    (hâ‚‚ : s âˆˆ lâ‚‚) (hinj : InjOn f s) : map f lâ‚ â‰¤ map f lâ‚‚ â†” lâ‚ â‰¤ lâ‚‚ :=
  âŸ¨fun h _t ht =>
    mp_mem hâ‚ <|
      mem_of_superset (h <| image_mem_map (inter_mem hâ‚‚ ht)) fun _y âŸ¨_x, âŸ¨hxs, hxtâŸ©, hxyâŸ© hys =>
        hinj hxs hys hxy â–¸ hxt,
    fun h => map_mono hâŸ©

theorem map_le_map_iff {f g : Filter Î±} {m : Î± â†’ Î²} (hm : Injective m) :
    map m f â‰¤ map m g â†” f â‰¤ g := by rw [map_le_iff_le_comap, comap_map hm]

theorem map_eq_map_iff_of_injOn {f g : Filter Î±} {m : Î± â†’ Î²} {s : Set Î±} (hsf : s âˆˆ f) (hsg : s âˆˆ g)
    (hm : InjOn m s) : map m f = map m g â†” f = g := by
  simp only [le_antisymm_iff, map_le_map_iff_of_injOn hsf hsg hm,
    map_le_map_iff_of_injOn hsg hsf hm]

theorem map_inj {f g : Filter Î±} {m : Î± â†’ Î²} (hm : Injective m) : map m f = map m g â†” f = g :=
  map_eq_map_iff_of_injOn univ_mem univ_mem hm.injOn

theorem map_injective {m : Î± â†’ Î²} (hm : Injective m) : Injective (map m) := fun _ _ =>
  (map_inj hm).1

theorem comap_neBot_iff {f : Filter Î²} {m : Î± â†’ Î²} : NeBot (comap m f) â†” âˆ€ t âˆˆ f, âˆƒ a, m a âˆˆ t := by
  simp only [â† forall_mem_nonempty_iff_neBot, mem_comap, forall_exists_index, and_imp]
  exact âŸ¨fun h t t_in => h (m â»Â¹' t) t t_in Subset.rfl, fun h s t ht hst => (h t ht).imp hstâŸ©

theorem comap_neBot {f : Filter Î²} {m : Î± â†’ Î²} (hm : âˆ€ t âˆˆ f, âˆƒ a, m a âˆˆ t) : NeBot (comap m f) :=
  comap_neBot_iff.mpr hm

theorem comap_neBot_iff_frequently {f : Filter Î²} {m : Î± â†’ Î²} :
    NeBot (comap m f) â†” âˆƒá¶  y in f, y âˆˆ range m := by
  simp only [comap_neBot_iff, frequently_iff, mem_range, @and_comm (_ âˆˆ _), exists_exists_eq_and]

theorem comap_neBot_iff_compl_range {f : Filter Î²} {m : Î± â†’ Î²} :
    NeBot (comap m f) â†” (range m)á¶œ âˆ‰ f :=
  comap_neBot_iff_frequently

theorem comap_eq_bot_iff_compl_range {f : Filter Î²} {m : Î± â†’ Î²} : comap m f = âŠ¥ â†” (range m)á¶œ âˆˆ f :=
  not_iff_not.mp <| neBot_iff.symm.trans comap_neBot_iff_compl_range

theorem comap_surjective_eq_bot {f : Filter Î²} {m : Î± â†’ Î²} (hm : Surjective m) :
    comap m f = âŠ¥ â†” f = âŠ¥ := by
  rw [comap_eq_bot_iff_compl_range, hm.range_eq, compl_univ, empty_mem_iff_bot]

theorem disjoint_comap_iff (h : Surjective m) :
    Disjoint (comap m gâ‚) (comap m gâ‚‚) â†” Disjoint gâ‚ gâ‚‚ := by
  rw [disjoint_iff, disjoint_iff, â† comap_inf, comap_surjective_eq_bot h]

theorem NeBot.comap_of_range_mem {f : Filter Î²} {m : Î± â†’ Î²} (_ : NeBot f) (hm : range m âˆˆ f) :
    NeBot (comap m f) :=
  comap_neBot_iff_frequently.2 <| Eventually.frequently hm

@[simp]
theorem comap_fst_neBot_iff {f : Filter Î±} :
    (f.comap (Prod.fst : Î± Ã— Î² â†’ Î±)).NeBot â†” f.NeBot âˆ§ Nonempty Î² := by
  cases isEmpty_or_nonempty Î²
  Â· rw [filter_eq_bot_of_isEmpty (f.comap _), â† not_iff_not]; simp [*]
  Â· simp [comap_neBot_iff_frequently, *]

@[instance]
theorem comap_fst_neBot [Nonempty Î²] {f : Filter Î±} [NeBot f] :
    (f.comap (Prod.fst : Î± Ã— Î² â†’ Î±)).NeBot :=
  comap_fst_neBot_iff.2 âŸ¨â€¹_â€º, â€¹_â€ºâŸ©

@[simp]
theorem comap_snd_neBot_iff {f : Filter Î²} :
    (f.comap (Prod.snd : Î± Ã— Î² â†’ Î²)).NeBot â†” Nonempty Î± âˆ§ f.NeBot := by
  cases' isEmpty_or_nonempty Î± with hÎ± hÎ±
  Â· rw [filter_eq_bot_of_isEmpty (f.comap _), â† not_iff_not]; simp
  Â· simp [comap_neBot_iff_frequently, hÎ±]

@[instance]
theorem comap_snd_neBot [Nonempty Î±] {f : Filter Î²} [NeBot f] :
    (f.comap (Prod.snd : Î± Ã— Î² â†’ Î²)).NeBot :=
  comap_snd_neBot_iff.2 âŸ¨â€¹_â€º, â€¹_â€ºâŸ©

theorem comap_eval_neBot_iff' {Î¹ : Type*} {Î± : Î¹ â†’ Type*} {i : Î¹} {f : Filter (Î± i)} :
    (comap (eval i) f).NeBot â†” (âˆ€ j, Nonempty (Î± j)) âˆ§ NeBot f := by
  cases' isEmpty_or_nonempty (âˆ€ j, Î± j) with H H
  Â· rw [filter_eq_bot_of_isEmpty (f.comap _), â† not_iff_not]
    simp [â† Classical.nonempty_pi]
  Â· have : âˆ€ j, Nonempty (Î± j) := Classical.nonempty_pi.1 H
    simp [comap_neBot_iff_frequently, *]

@[simp]
theorem comap_eval_neBot_iff {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [âˆ€ j, Nonempty (Î± j)] {i : Î¹}
    {f : Filter (Î± i)} : (comap (eval i) f).NeBot â†” NeBot f := by simp [comap_eval_neBot_iff', *]

@[instance]
theorem comap_eval_neBot {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [âˆ€ j, Nonempty (Î± j)] (i : Î¹)
    (f : Filter (Î± i)) [NeBot f] : (comap (eval i) f).NeBot :=
  comap_eval_neBot_iff.2 â€¹_â€º

theorem comap_coe_neBot_of_le_principal {s : Set Î³} {l : Filter Î³} [h : NeBot l] (h' : l â‰¤ ğ“Ÿ s) :
    NeBot (comap ((â†‘) : s â†’ Î³) l) :=
  h.comap_of_range_mem <| (@Subtype.range_coe Î³ s).symm â–¸ h' (mem_principal_self s)

theorem NeBot.comap_of_surj {f : Filter Î²} {m : Î± â†’ Î²} (hf : NeBot f) (hm : Surjective m) :
    NeBot (comap m f) :=
  hf.comap_of_range_mem <| univ_mem' hm

theorem NeBot.comap_of_image_mem {f : Filter Î²} {m : Î± â†’ Î²} (hf : NeBot f) {s : Set Î±}
    (hs : m '' s âˆˆ f) : NeBot (comap m f) :=
  hf.comap_of_range_mem <| mem_of_superset hs (image_subset_range _ _)

@[simp]
theorem map_eq_bot_iff : map m f = âŠ¥ â†” f = âŠ¥ :=
  âŸ¨by
    rw [â† empty_mem_iff_bot, â† empty_mem_iff_bot]
    exact id, fun h => by simp only [h, map_bot]âŸ©

theorem map_neBot_iff (f : Î± â†’ Î²) {F : Filter Î±} : NeBot (map f F) â†” NeBot F := by
  simp only [neBot_iff, Ne, map_eq_bot_iff]

theorem NeBot.map (hf : NeBot f) (m : Î± â†’ Î²) : NeBot (map m f) :=
  (map_neBot_iff m).2 hf

theorem NeBot.of_map : NeBot (f.map m) â†’ NeBot f :=
  (map_neBot_iff m).1

instance map_neBot [hf : NeBot f] : NeBot (f.map m) :=
  hf.map m

theorem sInter_comap_sets (f : Î± â†’ Î²) (F : Filter Î²) : â‹‚â‚€ (comap f F).sets = â‹‚ U âˆˆ F, f â»Â¹' U := by
  ext x
  suffices (âˆ€ (A : Set Î±) (B : Set Î²), B âˆˆ F â†’ f â»Â¹' B âŠ† A â†’ x âˆˆ A) â†”
      âˆ€ B : Set Î², B âˆˆ F â†’ f x âˆˆ B by
    simp only [mem_sInter, mem_iInter, Filter.mem_sets, mem_comap, this, and_imp, exists_prop,
      mem_preimage, exists_imp]
  constructor
  Â· intro h U U_in
    simpa only [Subset.rfl, forall_prop_of_true, mem_preimage] using h (f â»Â¹' U) U U_in
  Â· intro h V U U_in f_U_V
    exact f_U_V (h U U_in)

end Map

-- this is a generic rule for monotone functions:
theorem map_iInf_le {f : Î¹ â†’ Filter Î±} {m : Î± â†’ Î²} : map m (iInf f) â‰¤ â¨… i, map m (f i) :=
  le_iInf fun _ => map_mono <| iInf_le _ _

theorem map_iInf_eq {f : Î¹ â†’ Filter Î±} {m : Î± â†’ Î²} (hf : Directed (Â· â‰¥ Â·) f) [Nonempty Î¹] :
    map m (iInf f) = â¨… i, map m (f i) :=
  map_iInf_le.antisymm fun s (hs : m â»Â¹' s âˆˆ iInf f) =>
    let âŸ¨i, hiâŸ© := (mem_iInf_of_directed hf _).1 hs
    have : â¨… i, map m (f i) â‰¤ ğ“Ÿ s :=
      iInf_le_of_le i <| by simpa only [le_principal_iff, mem_map]
    Filter.le_principal_iff.1 this

theorem map_biInf_eq {Î¹ : Type w} {f : Î¹ â†’ Filter Î±} {m : Î± â†’ Î²} {p : Î¹ â†’ Prop}
    (h : DirectedOn (f â»Â¹'o (Â· â‰¥ Â·)) { x | p x }) (ne : âˆƒ i, p i) :
    map m (â¨… (i) (_ : p i), f i) = â¨… (i) (_ : p i), map m (f i) := by
  haveI := nonempty_subtype.2 ne
  simp only [iInf_subtype']
  exact map_iInf_eq h.directed_val

theorem map_inf_le {f g : Filter Î±} {m : Î± â†’ Î²} : map m (f âŠ“ g) â‰¤ map m f âŠ“ map m g :=
  (@map_mono _ _ m).map_inf_le f g

theorem map_inf {f g : Filter Î±} {m : Î± â†’ Î²} (h : Injective m) :
    map m (f âŠ“ g) = map m f âŠ“ map m g := by
  refine map_inf_le.antisymm ?_
  rintro t âŸ¨sâ‚, hsâ‚, sâ‚‚, hsâ‚‚, ht : m â»Â¹' t = sâ‚ âˆ© sâ‚‚âŸ©
  refine mem_inf_of_inter (image_mem_map hsâ‚) (image_mem_map hsâ‚‚) ?_
  rw [â† image_inter h, image_subset_iff, ht]

theorem map_inf' {f g : Filter Î±} {m : Î± â†’ Î²} {t : Set Î±} (htf : t âˆˆ f) (htg : t âˆˆ g)
    (h : InjOn m t) : map m (f âŠ“ g) = map m f âŠ“ map m g := by
  lift f to Filter t using htf; lift g to Filter t using htg
  replace h : Injective (m âˆ˜ ((â†‘) : t â†’ Î±)) := h.injective
  simp only [map_map, â† map_inf Subtype.coe_injective, map_inf h]

lemma disjoint_of_map {Î± Î² : Type*} {F G : Filter Î±} {f : Î± â†’ Î²}
    (h : Disjoint (map f F) (map f G)) : Disjoint F G :=
  disjoint_iff.mpr <| map_eq_bot_iff.mp <| le_bot_iff.mp <| trans map_inf_le (disjoint_iff.mp h)

theorem disjoint_map {m : Î± â†’ Î²} (hm : Injective m) {fâ‚ fâ‚‚ : Filter Î±} :
    Disjoint (map m fâ‚) (map m fâ‚‚) â†” Disjoint fâ‚ fâ‚‚ := by
  simp only [disjoint_iff, â† map_inf hm, map_eq_bot_iff]

theorem map_equiv_symm (e : Î± â‰ƒ Î²) (f : Filter Î²) : map e.symm f = comap e f :=
  map_injective e.injective <| by
    rw [map_map, e.self_comp_symm, map_id, map_comap_of_surjective e.surjective]

theorem map_eq_comap_of_inverse {f : Filter Î±} {m : Î± â†’ Î²} {n : Î² â†’ Î±} (hâ‚ : m âˆ˜ n = id)
    (hâ‚‚ : n âˆ˜ m = id) : map m f = comap n f :=
  map_equiv_symm âŸ¨n, m, congr_fun hâ‚, congr_fun hâ‚‚âŸ© f

theorem comap_equiv_symm (e : Î± â‰ƒ Î²) (f : Filter Î±) : comap e.symm f = map e f :=
  (map_eq_comap_of_inverse e.self_comp_symm e.symm_comp_self).symm

theorem map_swap_eq_comap_swap {f : Filter (Î± Ã— Î²)} : Prod.swap <$> f = comap Prod.swap f :=
  map_eq_comap_of_inverse Prod.swap_swap_eq Prod.swap_swap_eq

/-- A useful lemma when dealing with uniformities. -/
theorem map_swap4_eq_comap {f : Filter ((Î± Ã— Î²) Ã— Î³ Ã— Î´)} :
    map (fun p : (Î± Ã— Î²) Ã— Î³ Ã— Î´ => ((p.1.1, p.2.1), (p.1.2, p.2.2))) f =
      comap (fun p : (Î± Ã— Î³) Ã— Î² Ã— Î´ => ((p.1.1, p.2.1), (p.1.2, p.2.2))) f :=
  map_eq_comap_of_inverse (funext fun âŸ¨âŸ¨_, _âŸ©, âŸ¨_, _âŸ©âŸ© => rfl) (funext fun âŸ¨âŸ¨_, _âŸ©, âŸ¨_, _âŸ©âŸ© => rfl)

theorem le_map {f : Filter Î±} {m : Î± â†’ Î²} {g : Filter Î²} (h : âˆ€ s âˆˆ f, m '' s âˆˆ g) : g â‰¤ f.map m :=
  fun _ hs => mem_of_superset (h _ hs) <| image_preimage_subset _ _

theorem le_map_iff {f : Filter Î±} {m : Î± â†’ Î²} {g : Filter Î²} : g â‰¤ f.map m â†” âˆ€ s âˆˆ f, m '' s âˆˆ g :=
  âŸ¨fun h _ hs => h (image_mem_map hs), le_mapâŸ©

protected theorem push_pull (f : Î± â†’ Î²) (F : Filter Î±) (G : Filter Î²) :
    map f (F âŠ“ comap f G) = map f F âŠ“ G := by
  apply le_antisymm
  Â· calc
      map f (F âŠ“ comap f G) â‰¤ map f F âŠ“ (map f <| comap f G) := map_inf_le
      _ â‰¤ map f F âŠ“ G := inf_le_inf_left (map f F) map_comap_le

  Â· rintro U âŸ¨V, V_in, W, âŸ¨Z, Z_in, hZâŸ©, hâŸ©
    apply mem_inf_of_inter (image_mem_map V_in) Z_in
    calc
      f '' V âˆ© Z = f '' (V âˆ© f â»Â¹' Z) := by rw [image_inter_preimage]
      _ âŠ† f '' (V âˆ© W) := image_subset _ (inter_subset_inter_right _ â€¹_â€º)
      _ = f '' (f â»Â¹' U) := by rw [h]
      _ âŠ† U := image_preimage_subset f U

protected theorem push_pull' (f : Î± â†’ Î²) (F : Filter Î±) (G : Filter Î²) :
    map f (comap f G âŠ“ F) = G âŠ“ map f F := by simp only [Filter.push_pull, inf_comm]

theorem disjoint_comap_iff_map {f : Î± â†’ Î²} {F : Filter Î±} {G : Filter Î²} :
    Disjoint F (comap f G) â†” Disjoint (map f F) G := by
  simp only [disjoint_iff, â† Filter.push_pull, map_eq_bot_iff]

theorem disjoint_comap_iff_map' {f : Î± â†’ Î²} {F : Filter Î±} {G : Filter Î²} :
    Disjoint (comap f G) F â†” Disjoint G (map f F) := by
  simp only [disjoint_iff, â† Filter.push_pull', map_eq_bot_iff]

theorem neBot_inf_comap_iff_map {f : Î± â†’ Î²} {F : Filter Î±} {G : Filter Î²} :
    NeBot (F âŠ“ comap f G) â†” NeBot (map f F âŠ“ G) := by
  rw [â† map_neBot_iff, Filter.push_pull]

theorem neBot_inf_comap_iff_map' {f : Î± â†’ Î²} {F : Filter Î±} {G : Filter Î²} :
    NeBot (comap f G âŠ“ F) â†” NeBot (G âŠ“ map f F) := by
  rw [â† map_neBot_iff, Filter.push_pull']

theorem comap_inf_principal_neBot_of_image_mem {f : Filter Î²} {m : Î± â†’ Î²} (hf : NeBot f) {s : Set Î±}
    (hs : m '' s âˆˆ f) : NeBot (comap m f âŠ“ ğ“Ÿ s) := by
  rw [neBot_inf_comap_iff_map', map_principal, â† frequently_mem_iff_neBot]
  exact Eventually.frequently hs

theorem principal_eq_map_coe_top (s : Set Î±) : ğ“Ÿ s = map ((â†‘) : s â†’ Î±) âŠ¤ := by simp

theorem inf_principal_eq_bot_iff_comap {F : Filter Î±} {s : Set Î±} :
    F âŠ“ ğ“Ÿ s = âŠ¥ â†” comap ((â†‘) : s â†’ Î±) F = âŠ¥ := by
  rw [principal_eq_map_coe_top s, â† Filter.push_pull', inf_top_eq, map_eq_bot_iff]

section Applicative

theorem singleton_mem_pure {a : Î±} : {a} âˆˆ (pure a : Filter Î±) :=
  mem_singleton a

theorem pure_injective : Injective (pure : Î± â†’ Filter Î±) := fun a _ hab =>
  (Filter.ext_iff.1 hab { x | a = x }).1 rfl

instance pure_neBot {Î± : Type u} {a : Î±} : NeBot (pure a) :=
  âŸ¨mt empty_mem_iff_bot.2 <| not_mem_empty aâŸ©

@[simp]
theorem le_pure_iff {f : Filter Î±} {a : Î±} : f â‰¤ pure a â†” {a} âˆˆ f := by
  rw [â† principal_singleton, le_principal_iff]

theorem mem_seq_def {f : Filter (Î± â†’ Î²)} {g : Filter Î±} {s : Set Î²} :
    s âˆˆ f.seq g â†” âˆƒ u âˆˆ f, âˆƒ t âˆˆ g, âˆ€ x âˆˆ u, âˆ€ y âˆˆ t, (x : Î± â†’ Î²) y âˆˆ s :=
  Iff.rfl

theorem mem_seq_iff {f : Filter (Î± â†’ Î²)} {g : Filter Î±} {s : Set Î²} :
    s âˆˆ f.seq g â†” âˆƒ u âˆˆ f, âˆƒ t âˆˆ g, Set.seq u t âŠ† s := by
  simp only [mem_seq_def, seq_subset, exists_prop, iff_self_iff]

theorem mem_map_seq_iff {f : Filter Î±} {g : Filter Î²} {m : Î± â†’ Î² â†’ Î³} {s : Set Î³} :
    s âˆˆ (f.map m).seq g â†” âˆƒ t u, t âˆˆ g âˆ§ u âˆˆ f âˆ§ âˆ€ x âˆˆ u, âˆ€ y âˆˆ t, m x y âˆˆ s :=
  Iff.intro (fun âŸ¨t, ht, s, hs, htsâŸ© => âŸ¨s, m â»Â¹' t, hs, ht, fun _ => hts _âŸ©)
    fun âŸ¨t, s, ht, hs, htsâŸ© =>
    âŸ¨m '' s, image_mem_map hs, t, ht, fun _ âŸ¨_, has, EqâŸ© => Eq â–¸ hts _ hasâŸ©

theorem seq_mem_seq {f : Filter (Î± â†’ Î²)} {g : Filter Î±} {s : Set (Î± â†’ Î²)} {t : Set Î±} (hs : s âˆˆ f)
    (ht : t âˆˆ g) : s.seq t âˆˆ f.seq g :=
  âŸ¨s, hs, t, ht, fun f hf a ha => âŸ¨f, hf, a, ha, rflâŸ©âŸ©

theorem le_seq {f : Filter (Î± â†’ Î²)} {g : Filter Î±} {h : Filter Î²}
    (hh : âˆ€ t âˆˆ f, âˆ€ u âˆˆ g, Set.seq t u âˆˆ h) : h â‰¤ seq f g := fun _ âŸ¨_, ht, _, hu, hsâŸ© =>
  mem_of_superset (hh _ ht _ hu) fun _ âŸ¨_, hm, _, ha, eqâŸ© => eq â–¸ hs _ hm _ ha

@[mono]
theorem seq_mono {fâ‚ fâ‚‚ : Filter (Î± â†’ Î²)} {gâ‚ gâ‚‚ : Filter Î±} (hf : fâ‚ â‰¤ fâ‚‚) (hg : gâ‚ â‰¤ gâ‚‚) :
    fâ‚.seq gâ‚ â‰¤ fâ‚‚.seq gâ‚‚ :=
  le_seq fun _ hs _ ht => seq_mem_seq (hf hs) (hg ht)

@[simp]
theorem pure_seq_eq_map (g : Î± â†’ Î²) (f : Filter Î±) : seq (pure g) f = f.map g := by
  refine le_antisymm (le_map fun s hs => ?_) (le_seq fun s hs t ht => ?_)
  Â· rw [â† singleton_seq]
    apply seq_mem_seq _ hs
    exact singleton_mem_pure
  Â· refine sets_of_superset (map g f) (image_mem_map ht) ?_
    rintro b âŸ¨a, ha, rflâŸ©
    exact âŸ¨g, hs, a, ha, rflâŸ©

@[simp]
theorem seq_pure (f : Filter (Î± â†’ Î²)) (a : Î±) : seq f (pure a) = map (fun g : Î± â†’ Î² => g a) f := by
  refine le_antisymm (le_map fun s hs => ?_) (le_seq fun s hs t ht => ?_)
  Â· rw [â† seq_singleton]
    exact seq_mem_seq hs singleton_mem_pure
  Â· refine sets_of_superset (map (fun g : Î± â†’ Î² => g a) f) (image_mem_map hs) ?_
    rintro b âŸ¨g, hg, rflâŸ©
    exact âŸ¨g, hg, a, ht, rflâŸ©

@[simp]
theorem seq_assoc (x : Filter Î±) (g : Filter (Î± â†’ Î²)) (h : Filter (Î² â†’ Î³)) :
    seq h (seq g x) = seq (seq (map (Â· âˆ˜ Â·) h) g) x := by
  refine le_antisymm (le_seq fun s hs t ht => ?_) (le_seq fun s hs t ht => ?_)
  Â· rcases mem_seq_iff.1 hs with âŸ¨u, hu, v, hv, hsâŸ©
    rcases mem_map_iff_exists_image.1 hu with âŸ¨w, hw, huâŸ©
    refine mem_of_superset ?_ (Set.seq_mono ((Set.seq_mono hu Subset.rfl).trans hs) Subset.rfl)
    rw [â† Set.seq_seq]
    exact seq_mem_seq hw (seq_mem_seq hv ht)
  Â· rcases mem_seq_iff.1 ht with âŸ¨u, hu, v, hv, htâŸ©
    refine mem_of_superset ?_ (Set.seq_mono Subset.rfl ht)
    rw [Set.seq_seq]
    exact seq_mem_seq (seq_mem_seq (image_mem_map hs) hu) hv

theorem prod_map_seq_comm (f : Filter Î±) (g : Filter Î²) :
    (map Prod.mk f).seq g = seq (map (fun b a => (a, b)) g) f := by
  refine le_antisymm (le_seq fun s hs t ht => ?_) (le_seq fun s hs t ht => ?_)
  Â· rcases mem_map_iff_exists_image.1 hs with âŸ¨u, hu, hsâŸ©
    refine mem_of_superset ?_ (Set.seq_mono hs Subset.rfl)
    rw [â† Set.prod_image_seq_comm]
    exact seq_mem_seq (image_mem_map ht) hu
  Â· rcases mem_map_iff_exists_image.1 hs with âŸ¨u, hu, hsâŸ©
    refine mem_of_superset ?_ (Set.seq_mono hs Subset.rfl)
    rw [Set.prod_image_seq_comm]
    exact seq_mem_seq (image_mem_map ht) hu

theorem seq_eq_filter_seq {Î± Î² : Type u} (f : Filter (Î± â†’ Î²)) (g : Filter Î±) :
    f <*> g = seq f g :=
  rfl

instance : LawfulApplicative (Filter : Type u â†’ Type u) where
  map_pure := map_pure
  seqLeft_eq _ _ := rfl
  seqRight_eq _ _ := rfl
  seq_pure := seq_pure
  pure_seq := pure_seq_eq_map
  seq_assoc := seq_assoc

instance : CommApplicative (Filter : Type u â†’ Type u) :=
  âŸ¨fun f g => prod_map_seq_comm f gâŸ©

end Applicative

/-! #### `bind` equations -/


section Bind

@[simp]
theorem eventually_bind {f : Filter Î±} {m : Î± â†’ Filter Î²} {p : Î² â†’ Prop} :
    (âˆ€á¶  y in bind f m, p y) â†” âˆ€á¶  x in f, âˆ€á¶  y in m x, p y :=
  Iff.rfl

@[simp]
theorem eventuallyEq_bind {f : Filter Î±} {m : Î± â†’ Filter Î²} {gâ‚ gâ‚‚ : Î² â†’ Î³} :
    gâ‚ =á¶ [bind f m] gâ‚‚ â†” âˆ€á¶  x in f, gâ‚ =á¶ [m x] gâ‚‚ :=
  Iff.rfl

@[simp]
theorem eventuallyLE_bind [LE Î³] {f : Filter Î±} {m : Î± â†’ Filter Î²} {gâ‚ gâ‚‚ : Î² â†’ Î³} :
    gâ‚ â‰¤á¶ [bind f m] gâ‚‚ â†” âˆ€á¶  x in f, gâ‚ â‰¤á¶ [m x] gâ‚‚ :=
  Iff.rfl

theorem mem_bind' {s : Set Î²} {f : Filter Î±} {m : Î± â†’ Filter Î²} :
    s âˆˆ bind f m â†” { a | s âˆˆ m a } âˆˆ f :=
  Iff.rfl

@[simp]
theorem mem_bind {s : Set Î²} {f : Filter Î±} {m : Î± â†’ Filter Î²} :
    s âˆˆ bind f m â†” âˆƒ t âˆˆ f, âˆ€ x âˆˆ t, s âˆˆ m x :=
  calc
    s âˆˆ bind f m â†” { a | s âˆˆ m a } âˆˆ f := Iff.rfl
    _ â†” âˆƒ t âˆˆ f, t âŠ† { a | s âˆˆ m a } := exists_mem_subset_iff.symm
    _ â†” âˆƒ t âˆˆ f, âˆ€ x âˆˆ t, s âˆˆ m x := Iff.rfl

theorem bind_le {f : Filter Î±} {g : Î± â†’ Filter Î²} {l : Filter Î²} (h : âˆ€á¶  x in f, g x â‰¤ l) :
    f.bind g â‰¤ l :=
  join_le <| eventually_map.2 h

@[mono]
theorem bind_mono {fâ‚ fâ‚‚ : Filter Î±} {gâ‚ gâ‚‚ : Î± â†’ Filter Î²} (hf : fâ‚ â‰¤ fâ‚‚) (hg : gâ‚ â‰¤á¶ [fâ‚] gâ‚‚) :
    bind fâ‚ gâ‚ â‰¤ bind fâ‚‚ gâ‚‚ := by
  refine le_trans (fun s hs => ?_) (join_mono <| map_mono hf)
  simp only [mem_join, mem_bind', mem_map] at hs âŠ¢
  filter_upwards [hg, hs] with _ hx hs using hx hs

theorem bind_inf_principal {f : Filter Î±} {g : Î± â†’ Filter Î²} {s : Set Î²} :
    (f.bind fun x => g x âŠ“ ğ“Ÿ s) = f.bind g âŠ“ ğ“Ÿ s :=
  Filter.ext fun s => by simp only [mem_bind, mem_inf_principal]

theorem sup_bind {f g : Filter Î±} {h : Î± â†’ Filter Î²} : bind (f âŠ” g) h = bind f h âŠ” bind g h := rfl

theorem principal_bind {s : Set Î±} {f : Î± â†’ Filter Î²} : bind (ğ“Ÿ s) f = â¨† x âˆˆ s, f x :=
  show join (map f (ğ“Ÿ s)) = â¨† x âˆˆ s, f x by
    simp only [sSup_image, join_principal_eq_sSup, map_principal, eq_self_iff_true]

end Bind

/-! ### Limits -/

/-- `Filter.Tendsto` is the generic "limit of a function" predicate.
  `Tendsto f lâ‚ lâ‚‚` asserts that for every `lâ‚‚` neighborhood `a`,
  the `f`-preimage of `a` is an `lâ‚` neighborhood. -/
def Tendsto (f : Î± â†’ Î²) (lâ‚ : Filter Î±) (lâ‚‚ : Filter Î²) :=
  lâ‚.map f â‰¤ lâ‚‚

theorem tendsto_def {f : Î± â†’ Î²} {lâ‚ : Filter Î±} {lâ‚‚ : Filter Î²} :
    Tendsto f lâ‚ lâ‚‚ â†” âˆ€ s âˆˆ lâ‚‚, f â»Â¹' s âˆˆ lâ‚ :=
  Iff.rfl

theorem tendsto_iff_eventually {f : Î± â†’ Î²} {lâ‚ : Filter Î±} {lâ‚‚ : Filter Î²} :
    Tendsto f lâ‚ lâ‚‚ â†” âˆ€ â¦ƒp : Î² â†’ Propâ¦„, (âˆ€á¶  y in lâ‚‚, p y) â†’ âˆ€á¶  x in lâ‚, p (f x) :=
  Iff.rfl

theorem tendsto_iff_forall_eventually_mem {f : Î± â†’ Î²} {lâ‚ : Filter Î±} {lâ‚‚ : Filter Î²} :
    Tendsto f lâ‚ lâ‚‚ â†” âˆ€ s âˆˆ lâ‚‚, âˆ€á¶  x in lâ‚, f x âˆˆ s :=
  Iff.rfl

lemma Tendsto.eventually_mem {f : Î± â†’ Î²} {lâ‚ : Filter Î±} {lâ‚‚ : Filter Î²} {s : Set Î²}
    (hf : Tendsto f lâ‚ lâ‚‚) (h : s âˆˆ lâ‚‚) : âˆ€á¶  x in lâ‚, f x âˆˆ s :=
  hf h

theorem Tendsto.eventually {f : Î± â†’ Î²} {lâ‚ : Filter Î±} {lâ‚‚ : Filter Î²} {p : Î² â†’ Prop}
    (hf : Tendsto f lâ‚ lâ‚‚) (h : âˆ€á¶  y in lâ‚‚, p y) : âˆ€á¶  x in lâ‚, p (f x) :=
  hf h

theorem not_tendsto_iff_exists_frequently_nmem {f : Î± â†’ Î²} {lâ‚ : Filter Î±} {lâ‚‚ : Filter Î²} :
    Â¬Tendsto f lâ‚ lâ‚‚ â†” âˆƒ s âˆˆ lâ‚‚, âˆƒá¶  x in lâ‚, f x âˆ‰ s := by
  simp only [tendsto_iff_forall_eventually_mem, not_forall, exists_prop, not_eventually]

theorem Tendsto.frequently {f : Î± â†’ Î²} {lâ‚ : Filter Î±} {lâ‚‚ : Filter Î²} {p : Î² â†’ Prop}
    (hf : Tendsto f lâ‚ lâ‚‚) (h : âˆƒá¶  x in lâ‚, p (f x)) : âˆƒá¶  y in lâ‚‚, p y :=
  mt hf.eventually h

theorem Tendsto.frequently_map {lâ‚ : Filter Î±} {lâ‚‚ : Filter Î²} {p : Î± â†’ Prop} {q : Î² â†’ Prop}
    (f : Î± â†’ Î²) (c : Filter.Tendsto f lâ‚ lâ‚‚) (w : âˆ€ x, p x â†’ q (f x)) (h : âˆƒá¶  x in lâ‚, p x) :
    âˆƒá¶  y in lâ‚‚, q y :=
  c.frequently (h.mono w)

@[simp]
theorem tendsto_bot {f : Î± â†’ Î²} {l : Filter Î²} : Tendsto f âŠ¥ l := by simp [Tendsto]

@[simp] theorem tendsto_top {f : Î± â†’ Î²} {l : Filter Î±} : Tendsto f l âŠ¤ := le_top

theorem le_map_of_right_inverse {mab : Î± â†’ Î²} {mba : Î² â†’ Î±} {f : Filter Î±} {g : Filter Î²}
    (hâ‚ : mab âˆ˜ mba =á¶ [g] id) (hâ‚‚ : Tendsto mba g f) : g â‰¤ map mab f := by
  rw [â† @map_id _ g, â† map_congr hâ‚, â† map_map]
  exact map_mono hâ‚‚

theorem tendsto_of_isEmpty [IsEmpty Î±] {f : Î± â†’ Î²} {la : Filter Î±} {lb : Filter Î²} :
    Tendsto f la lb := by simp only [filter_eq_bot_of_isEmpty la, tendsto_bot]

theorem eventuallyEq_of_left_inv_of_right_inv {f : Î± â†’ Î²} {gâ‚ gâ‚‚ : Î² â†’ Î±} {fa : Filter Î±}
    {fb : Filter Î²} (hleft : âˆ€á¶  x in fa, gâ‚ (f x) = x) (hright : âˆ€á¶  y in fb, f (gâ‚‚ y) = y)
    (htendsto : Tendsto gâ‚‚ fb fa) : gâ‚ =á¶ [fb] gâ‚‚ :=
  (htendsto.eventually hleft).mp <| hright.mono fun _ hr hl => (congr_arg gâ‚ hr.symm).trans hl

theorem tendsto_iff_comap {f : Î± â†’ Î²} {lâ‚ : Filter Î±} {lâ‚‚ : Filter Î²} :
    Tendsto f lâ‚ lâ‚‚ â†” lâ‚ â‰¤ lâ‚‚.comap f :=
  map_le_iff_le_comap

alias âŸ¨Tendsto.le_comap, _âŸ© := tendsto_iff_comap

protected theorem Tendsto.disjoint {f : Î± â†’ Î²} {laâ‚ laâ‚‚ : Filter Î±} {lbâ‚ lbâ‚‚ : Filter Î²}
    (hâ‚ : Tendsto f laâ‚ lbâ‚) (hd : Disjoint lbâ‚ lbâ‚‚) (hâ‚‚ : Tendsto f laâ‚‚ lbâ‚‚) : Disjoint laâ‚ laâ‚‚ :=
  (disjoint_comap hd).mono hâ‚.le_comap hâ‚‚.le_comap

theorem tendsto_congr' {fâ‚ fâ‚‚ : Î± â†’ Î²} {lâ‚ : Filter Î±} {lâ‚‚ : Filter Î²} (hl : fâ‚ =á¶ [lâ‚] fâ‚‚) :
    Tendsto fâ‚ lâ‚ lâ‚‚ â†” Tendsto fâ‚‚ lâ‚ lâ‚‚ := by rw [Tendsto, Tendsto, map_congr hl]

theorem Tendsto.congr' {fâ‚ fâ‚‚ : Î± â†’ Î²} {lâ‚ : Filter Î±} {lâ‚‚ : Filter Î²} (hl : fâ‚ =á¶ [lâ‚] fâ‚‚)
    (h : Tendsto fâ‚ lâ‚ lâ‚‚) : Tendsto fâ‚‚ lâ‚ lâ‚‚ :=
  (tendsto_congr' hl).1 h

theorem tendsto_congr {fâ‚ fâ‚‚ : Î± â†’ Î²} {lâ‚ : Filter Î±} {lâ‚‚ : Filter Î²} (h : âˆ€ x, fâ‚ x = fâ‚‚ x) :
    Tendsto fâ‚ lâ‚ lâ‚‚ â†” Tendsto fâ‚‚ lâ‚ lâ‚‚ :=
  tendsto_congr' (univ_mem' h)

theorem Tendsto.congr {fâ‚ fâ‚‚ : Î± â†’ Î²} {lâ‚ : Filter Î±} {lâ‚‚ : Filter Î²} (h : âˆ€ x, fâ‚ x = fâ‚‚ x) :
    Tendsto fâ‚ lâ‚ lâ‚‚ â†’ Tendsto fâ‚‚ lâ‚ lâ‚‚ :=
  (tendsto_congr h).1

theorem tendsto_id' {x y : Filter Î±} : Tendsto id x y â†” x â‰¤ y :=
  Iff.rfl

theorem tendsto_id {x : Filter Î±} : Tendsto id x x :=
  le_refl x

theorem Tendsto.comp {f : Î± â†’ Î²} {g : Î² â†’ Î³} {x : Filter Î±} {y : Filter Î²} {z : Filter Î³}
    (hg : Tendsto g y z) (hf : Tendsto f x y) : Tendsto (g âˆ˜ f) x z := fun _ hs => hf (hg hs)

protected theorem Tendsto.iterate {f : Î± â†’ Î±} {l : Filter Î±} (h : Tendsto f l l) :
    âˆ€ n, Tendsto (f^[n]) l l
  | 0 => tendsto_id
  | (n + 1) => (h.iterate n).comp h

theorem Tendsto.mono_left {f : Î± â†’ Î²} {x y : Filter Î±} {z : Filter Î²} (hx : Tendsto f x z)
    (h : y â‰¤ x) : Tendsto f y z :=
  (map_mono h).trans hx

theorem Tendsto.mono_right {f : Î± â†’ Î²} {x : Filter Î±} {y z : Filter Î²} (hy : Tendsto f x y)
    (hz : y â‰¤ z) : Tendsto f x z :=
  le_trans hy hz

theorem Tendsto.neBot {f : Î± â†’ Î²} {x : Filter Î±} {y : Filter Î²} (h : Tendsto f x y) [hx : NeBot x] :
    NeBot y :=
  (hx.map _).mono h

theorem tendsto_map {f : Î± â†’ Î²} {x : Filter Î±} : Tendsto f x (map f x) :=
  le_refl (map f x)

@[simp]
theorem tendsto_map'_iff {f : Î² â†’ Î³} {g : Î± â†’ Î²} {x : Filter Î±} {y : Filter Î³} :
    Tendsto f (map g x) y â†” Tendsto (f âˆ˜ g) x y := by
  rw [Tendsto, Tendsto, map_map]

alias âŸ¨_, tendsto_map'âŸ© := tendsto_map'_iff

theorem tendsto_comap {f : Î± â†’ Î²} {x : Filter Î²} : Tendsto f (comap f x) x :=
  map_comap_le

@[simp]
theorem tendsto_comap_iff {f : Î± â†’ Î²} {g : Î² â†’ Î³} {a : Filter Î±} {c : Filter Î³} :
    Tendsto f a (c.comap g) â†” Tendsto (g âˆ˜ f) a c :=
  âŸ¨fun h => tendsto_comap.comp h, fun h => map_le_iff_le_comap.mp <| by rwa [map_map]âŸ©

theorem tendsto_comap'_iff {m : Î± â†’ Î²} {f : Filter Î±} {g : Filter Î²} {i : Î³ â†’ Î±} (h : range i âˆˆ f) :
    Tendsto (m âˆ˜ i) (comap i f) g â†” Tendsto m f g := by
  rw [Tendsto, â† map_compose]
  simp only [(Â· âˆ˜ Â·), map_comap_of_mem h, Tendsto]

theorem Tendsto.of_tendsto_comp {f : Î± â†’ Î²} {g : Î² â†’ Î³} {a : Filter Î±} {b : Filter Î²} {c : Filter Î³}
    (hfg : Tendsto (g âˆ˜ f) a c) (hg : comap g c â‰¤ b) : Tendsto f a b := by
  rw [tendsto_iff_comap] at hfg âŠ¢
  calc
    a â‰¤ comap (g âˆ˜ f) c := hfg
    _ â‰¤ comap f b := by simpa [comap_comap] using comap_mono hg

theorem comap_eq_of_inverse {f : Filter Î±} {g : Filter Î²} {Ï† : Î± â†’ Î²} (Ïˆ : Î² â†’ Î±) (eq : Ïˆ âˆ˜ Ï† = id)
    (hÏ† : Tendsto Ï† f g) (hÏˆ : Tendsto Ïˆ g f) : comap Ï† g = f := by
  refine ((comap_mono <| map_le_iff_le_comap.1 hÏˆ).trans ?_).antisymm (map_le_iff_le_comap.1 hÏ†)
  rw [comap_comap, eq, comap_id]

theorem map_eq_of_inverse {f : Filter Î±} {g : Filter Î²} {Ï† : Î± â†’ Î²} (Ïˆ : Î² â†’ Î±) (eq : Ï† âˆ˜ Ïˆ = id)
    (hÏ† : Tendsto Ï† f g) (hÏˆ : Tendsto Ïˆ g f) : map Ï† f = g := by
  refine le_antisymm hÏ† (le_trans ?_ (map_mono hÏˆ))
  rw [map_map, eq, map_id]

theorem tendsto_inf {f : Î± â†’ Î²} {x : Filter Î±} {yâ‚ yâ‚‚ : Filter Î²} :
    Tendsto f x (yâ‚ âŠ“ yâ‚‚) â†” Tendsto f x yâ‚ âˆ§ Tendsto f x yâ‚‚ := by
  simp only [Tendsto, le_inf_iff, iff_self_iff]

theorem tendsto_inf_left {f : Î± â†’ Î²} {xâ‚ xâ‚‚ : Filter Î±} {y : Filter Î²} (h : Tendsto f xâ‚ y) :
    Tendsto f (xâ‚ âŠ“ xâ‚‚) y :=
  le_trans (map_mono inf_le_left) h

theorem tendsto_inf_right {f : Î± â†’ Î²} {xâ‚ xâ‚‚ : Filter Î±} {y : Filter Î²} (h : Tendsto f xâ‚‚ y) :
    Tendsto f (xâ‚ âŠ“ xâ‚‚) y :=
  le_trans (map_mono inf_le_right) h

theorem Tendsto.inf {f : Î± â†’ Î²} {xâ‚ xâ‚‚ : Filter Î±} {yâ‚ yâ‚‚ : Filter Î²} (hâ‚ : Tendsto f xâ‚ yâ‚)
    (hâ‚‚ : Tendsto f xâ‚‚ yâ‚‚) : Tendsto f (xâ‚ âŠ“ xâ‚‚) (yâ‚ âŠ“ yâ‚‚) :=
  tendsto_inf.2 âŸ¨tendsto_inf_left hâ‚, tendsto_inf_right hâ‚‚âŸ©

@[simp]
theorem tendsto_iInf {f : Î± â†’ Î²} {x : Filter Î±} {y : Î¹ â†’ Filter Î²} :
    Tendsto f x (â¨… i, y i) â†” âˆ€ i, Tendsto f x (y i) := by
  simp only [Tendsto, iff_self_iff, le_iInf_iff]

theorem tendsto_iInf' {f : Î± â†’ Î²} {x : Î¹ â†’ Filter Î±} {y : Filter Î²} (i : Î¹)
    (hi : Tendsto f (x i) y) : Tendsto f (â¨… i, x i) y :=
  hi.mono_left <| iInf_le _ _

theorem tendsto_iInf_iInf {f : Î± â†’ Î²} {x : Î¹ â†’ Filter Î±} {y : Î¹ â†’ Filter Î²}
    (h : âˆ€ i, Tendsto f (x i) (y i)) : Tendsto f (iInf x) (iInf y) :=
  tendsto_iInf.2 fun i => tendsto_iInf' i (h i)

@[simp]
theorem tendsto_sup {f : Î± â†’ Î²} {xâ‚ xâ‚‚ : Filter Î±} {y : Filter Î²} :
    Tendsto f (xâ‚ âŠ” xâ‚‚) y â†” Tendsto f xâ‚ y âˆ§ Tendsto f xâ‚‚ y := by
  simp only [Tendsto, map_sup, sup_le_iff]

theorem Tendsto.sup {f : Î± â†’ Î²} {xâ‚ xâ‚‚ : Filter Î±} {y : Filter Î²} :
    Tendsto f xâ‚ y â†’ Tendsto f xâ‚‚ y â†’ Tendsto f (xâ‚ âŠ” xâ‚‚) y := fun hâ‚ hâ‚‚ => tendsto_sup.mpr âŸ¨hâ‚, hâ‚‚âŸ©

theorem Tendsto.sup_sup {f : Î± â†’ Î²} {xâ‚ xâ‚‚ : Filter Î±} {yâ‚ yâ‚‚ : Filter Î²}
    (hâ‚ : Tendsto f xâ‚ yâ‚) (hâ‚‚ : Tendsto f xâ‚‚ yâ‚‚) : Tendsto f (xâ‚ âŠ” xâ‚‚) (yâ‚ âŠ” yâ‚‚) :=
  tendsto_sup.mpr âŸ¨hâ‚.mono_right le_sup_left, hâ‚‚.mono_right le_sup_rightâŸ©

@[simp]
theorem tendsto_iSup {f : Î± â†’ Î²} {x : Î¹ â†’ Filter Î±} {y : Filter Î²} :
    Tendsto f (â¨† i, x i) y â†” âˆ€ i, Tendsto f (x i) y := by simp only [Tendsto, map_iSup, iSup_le_iff]

theorem tendsto_iSup_iSup {f : Î± â†’ Î²} {x : Î¹ â†’ Filter Î±} {y : Î¹ â†’ Filter Î²}
    (h : âˆ€ i, Tendsto f (x i) (y i)) : Tendsto f (iSup x) (iSup y) :=
  tendsto_iSup.2 fun i => (h i).mono_right <| le_iSup _ _

@[simp] theorem tendsto_principal {f : Î± â†’ Î²} {l : Filter Î±} {s : Set Î²} :
    Tendsto f l (ğ“Ÿ s) â†” âˆ€á¶  a in l, f a âˆˆ s := by
  simp only [Tendsto, le_principal_iff, mem_map', Filter.Eventually]

-- Porting note: was a `simp` lemma
theorem tendsto_principal_principal {f : Î± â†’ Î²} {s : Set Î±} {t : Set Î²} :
    Tendsto f (ğ“Ÿ s) (ğ“Ÿ t) â†” âˆ€ a âˆˆ s, f a âˆˆ t := by
  simp only [tendsto_principal, eventually_principal]

@[simp] theorem tendsto_pure {f : Î± â†’ Î²} {a : Filter Î±} {b : Î²} :
    Tendsto f a (pure b) â†” âˆ€á¶  x in a, f x = b := by
  simp only [Tendsto, le_pure_iff, mem_map', mem_singleton_iff, Filter.Eventually]

theorem tendsto_pure_pure (f : Î± â†’ Î²) (a : Î±) : Tendsto f (pure a) (pure (f a)) :=
  tendsto_pure.2 rfl

theorem tendsto_const_pure {a : Filter Î±} {b : Î²} : Tendsto (fun _ => b) a (pure b) :=
  tendsto_pure.2 <| univ_mem' fun _ => rfl

theorem pure_le_iff {a : Î±} {l : Filter Î±} : pure a â‰¤ l â†” âˆ€ s âˆˆ l, a âˆˆ s :=
  Iff.rfl

theorem tendsto_pure_left {f : Î± â†’ Î²} {a : Î±} {l : Filter Î²} :
    Tendsto f (pure a) l â†” âˆ€ s âˆˆ l, f a âˆˆ s :=
  Iff.rfl

@[simp]
theorem map_inf_principal_preimage {f : Î± â†’ Î²} {s : Set Î²} {l : Filter Î±} :
    map f (l âŠ“ ğ“Ÿ (f â»Â¹' s)) = map f l âŠ“ ğ“Ÿ s :=
  Filter.ext fun t => by simp only [mem_map', mem_inf_principal, mem_setOf_eq, mem_preimage]

/-- If two filters are disjoint, then a function cannot tend to both of them along a non-trivial
filter. -/
theorem Tendsto.not_tendsto {f : Î± â†’ Î²} {a : Filter Î±} {bâ‚ bâ‚‚ : Filter Î²} (hf : Tendsto f a bâ‚)
    [NeBot a] (hb : Disjoint bâ‚ bâ‚‚) : Â¬Tendsto f a bâ‚‚ := fun hf' =>
  (tendsto_inf.2 âŸ¨hf, hf'âŸ©).neBot.ne hb.eq_bot

protected theorem Tendsto.if {lâ‚ : Filter Î±} {lâ‚‚ : Filter Î²} {f g : Î± â†’ Î²} {p : Î± â†’ Prop}
    [âˆ€ x, Decidable (p x)] (hâ‚€ : Tendsto f (lâ‚ âŠ“ ğ“Ÿ { x | p x }) lâ‚‚)
    (hâ‚ : Tendsto g (lâ‚ âŠ“ ğ“Ÿ { x | Â¬p x }) lâ‚‚) :
    Tendsto (fun x => if p x then f x else g x) lâ‚ lâ‚‚ := by
  simp only [tendsto_def, mem_inf_principal] at *
  intro s hs
  filter_upwards [hâ‚€ s hs, hâ‚ s hs] with x hpâ‚€ hpâ‚
  rw [mem_preimage]
  split_ifs with h
  exacts [hpâ‚€ h, hpâ‚ h]

protected theorem Tendsto.if' {Î± Î² : Type*} {lâ‚ : Filter Î±} {lâ‚‚ : Filter Î²} {f g : Î± â†’ Î²}
    {p : Î± â†’ Prop} [DecidablePred p] (hf : Tendsto f lâ‚ lâ‚‚) (hg : Tendsto g lâ‚ lâ‚‚) :
    Tendsto (fun a => if p a then f a else g a) lâ‚ lâ‚‚ :=
  (tendsto_inf_left hf).if (tendsto_inf_left hg)

protected theorem Tendsto.piecewise {lâ‚ : Filter Î±} {lâ‚‚ : Filter Î²} {f g : Î± â†’ Î²} {s : Set Î±}
    [âˆ€ x, Decidable (x âˆˆ s)] (hâ‚€ : Tendsto f (lâ‚ âŠ“ ğ“Ÿ s) lâ‚‚) (hâ‚ : Tendsto g (lâ‚ âŠ“ ğ“Ÿ sá¶œ) lâ‚‚) :
    Tendsto (piecewise s f g) lâ‚ lâ‚‚ :=
  Tendsto.if hâ‚€ hâ‚

end Filter

open Filter

theorem Set.EqOn.eventuallyEq {Î± Î²} {s : Set Î±} {f g : Î± â†’ Î²} (h : EqOn f g s) : f =á¶ [ğ“Ÿ s] g :=
  h

theorem Set.EqOn.eventuallyEq_of_mem {Î± Î²} {s : Set Î±} {l : Filter Î±} {f g : Î± â†’ Î²} (h : EqOn f g s)
    (hl : s âˆˆ l) : f =á¶ [l] g :=
  h.eventuallyEq.filter_mono <| Filter.le_principal_iff.2 hl

theorem HasSubset.Subset.eventuallyLE {Î±} {l : Filter Î±} {s t : Set Î±} (h : s âŠ† t) : s â‰¤á¶ [l] t :=
  Filter.Eventually.of_forall h

theorem Set.MapsTo.tendsto {Î± Î²} {s : Set Î±} {t : Set Î²} {f : Î± â†’ Î²} (h : MapsTo f s t) :
    Filter.Tendsto f (ğ“Ÿ s) (ğ“Ÿ t) :=
  Filter.tendsto_principal_principal.2 h

theorem Filter.EventuallyEq.comp_tendsto {Î± Î² Î³ : Type*} {l : Filter Î±} {f : Î± â†’ Î²} {f' : Î± â†’ Î²}
    (H : f =á¶ [l] f') {g : Î³ â†’ Î±} {lc : Filter Î³} (hg : Tendsto g lc l) :
    f âˆ˜ g =á¶ [lc] f' âˆ˜ g :=
  hg.eventually H

variable {Î± Î² : Type*} {F : Filter Î±} {G : Filter Î²}

theorem Filter.map_mapsTo_Iic_iff_tendsto {m : Î± â†’ Î²} :
    MapsTo (map m) (Iic F) (Iic G) â†” Tendsto m F G :=
  âŸ¨fun hm â†¦ hm right_mem_Iic, fun hm _ â†¦ hm.mono_leftâŸ©

alias âŸ¨_, Filter.Tendsto.map_mapsTo_IicâŸ© := Filter.map_mapsTo_Iic_iff_tendsto

theorem Filter.map_mapsTo_Iic_iff_mapsTo {s : Set Î±} {t : Set Î²} {m : Î± â†’ Î²} :
    MapsTo (map m) (Iic <| ğ“Ÿ s) (Iic <| ğ“Ÿ t) â†” MapsTo m s t := by
  rw [map_mapsTo_Iic_iff_tendsto, tendsto_principal_principal, MapsTo]

alias âŸ¨_, Set.MapsTo.filter_map_IicâŸ© := Filter.map_mapsTo_Iic_iff_mapsTo

-- TODO(Anatole): unify with the global case
theorem Filter.map_surjOn_Iic_iff_le_map {m : Î± â†’ Î²} :
    SurjOn (map m) (Iic F) (Iic G) â†” G â‰¤ map m F := by
  refine âŸ¨fun hm â†¦ ?_, fun hm â†¦ ?_âŸ©
  Â· rcases hm right_mem_Iic with âŸ¨H, (hHF : H â‰¤ F), rflâŸ©
    exact map_mono hHF
  Â· have : RightInvOn (F âŠ“ comap m Â·) (map m) (Iic G) :=
      fun H (hHG : H â‰¤ G) â†¦ by simpa [Filter.push_pull] using hHG.trans hm
    exact this.surjOn fun H _ â†¦ mem_Iic.mpr inf_le_left

theorem Filter.map_surjOn_Iic_iff_surjOn {s : Set Î±} {t : Set Î²} {m : Î± â†’ Î²} :
    SurjOn (map m) (Iic <| ğ“Ÿ s) (Iic <| ğ“Ÿ t) â†” SurjOn m s t := by
  rw [map_surjOn_Iic_iff_le_map, map_principal, principal_mono, SurjOn]

alias âŸ¨_, Set.SurjOn.filter_map_IicâŸ© := Filter.map_surjOn_Iic_iff_surjOn

theorem Filter.filter_injOn_Iic_iff_injOn {s : Set Î±} {m : Î± â†’ Î²} :
    InjOn (map m) (Iic <| ğ“Ÿ s) â†” InjOn m s := by
  refine âŸ¨fun hm x hx y hy hxy â†¦ ?_, fun hm F hF G hG â†¦ ?_âŸ©
  Â· rwa [â† pure_injective.eq_iff, â† map_pure, â† map_pure, hm.eq_iff, pure_injective.eq_iff]
      at hxy <;> rwa [mem_Iic, pure_le_principal]
  Â· simp [map_eq_map_iff_of_injOn (le_principal_iff.mp hF) (le_principal_iff.mp hG) hm]

alias âŸ¨_, Set.InjOn.filter_map_IicâŸ© := Filter.filter_injOn_Iic_iff_injOn

namespace Filter

/-- Construct a filter from a property that is stable under finite unions.
A set `s` belongs to `Filter.comk p _ _ _` iff its complement satisfies the predicate `p`.
This constructor is useful to define filters like `Filter.cofinite`. -/
def comk (p : Set Î± â†’ Prop) (he : p âˆ…) (hmono : âˆ€ t, p t â†’ âˆ€ s âŠ† t, p s)
    (hunion : âˆ€ s, p s â†’ âˆ€ t, p t â†’ p (s âˆª t)) : Filter Î± where
  sets := {t | p tá¶œ}
  univ_sets := by simpa
  sets_of_superset := fun htâ‚ ht => hmono _ htâ‚ _ (compl_subset_compl.2 ht)
  inter_sets := fun htâ‚ htâ‚‚ => by simp [compl_inter, hunion _ htâ‚ _ htâ‚‚]

@[simp]
lemma mem_comk {p : Set Î± â†’ Prop} {he hmono hunion s} :
    s âˆˆ comk p he hmono hunion â†” p sá¶œ :=
  .rfl

lemma compl_mem_comk {p : Set Î± â†’ Prop} {he hmono hunion s} :
    sá¶œ âˆˆ comk p he hmono hunion â†” p s := by
  simp

end Filter

set_option linter.style.longFile 3000
