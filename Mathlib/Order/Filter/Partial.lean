/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad
-/
import Mathlib.Order.Filter.Tendsto
import Mathlib.Data.PFun

/-!
# `Tendsto` for relations and partial functions

This file generalizes `Filter` definitions from functions to partial functions and relations.

## Considering functions and partial functions as relations

A function `f : Œ± ‚Üí Œ≤` can be considered as the relation `Rel Œ± Œ≤` which relates `x` and `f x` for
all `x`, and nothing else. This relation is called `Function.Graph f`.

A partial function `f : Œ± ‚Üí. Œ≤` can be considered as the relation `Rel Œ± Œ≤` which relates `x` and
`f x` for all `x` for which `f x` exists, and nothing else. This relation is called
`PFun.Graph' f`.

In this regard, a function is a relation for which every element in `Œ±` is related to exactly one
element in `Œ≤` and a partial function is a relation for which every element in `Œ±` is related to at
most one element in `Œ≤`.

This file leverages this analogy to generalize `Filter` definitions from functions to partial
functions and relations.

## Notes

`Set.preimage` can be generalized to relations in two ways:
* `Rel.preimage` returns the image of the set under the inverse relation.
* `Rel.core` returns the set of elements that are only related to those in the set.
Both generalizations are sensible in the context of filters, so `Filter.comap` and `Filter.Tendsto`
get two generalizations each.

We first take care of relations. Then the definitions for partial functions are taken as special
cases of the definitions for relations.
-/


universe u v w

namespace Filter

variable {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w}

open Filter

/-! ### Relations -/


/-- The forward map of a filter under a relation. Generalization of `Filter.map` to relations. Note
that `Rel.core` generalizes `Set.preimage`. -/
def rmap (r : SetRel Œ± Œ≤) (l : Filter Œ±) : Filter Œ≤ where
  sets := { s | r.core s ‚àà l }
  univ_sets := by simp
  sets_of_superset hs st := mem_of_superset hs (SetRel.core_mono st)
  inter_sets hs ht := by
    simp only [Set.mem_setOf_eq]
    convert inter_mem hs ht
    rw [‚Üê SetRel.core_inter]

theorem rmap_sets (r : SetRel Œ± Œ≤) (l : Filter Œ±) : (l.rmap r).sets = r.core ‚Åª¬π' l.sets :=
  rfl

@[simp]
theorem mem_rmap (r : SetRel Œ± Œ≤) (l : Filter Œ±) (s : Set Œ≤) : s ‚àà l.rmap r ‚Üî r.core s ‚àà l :=
  Iff.rfl

@[simp]
theorem rmap_rmap (r : SetRel Œ± Œ≤) (s : SetRel Œ≤ Œ≥) (l : Filter Œ±) :
    rmap s (rmap r l) = rmap (r.comp s) l :=
  filter_eq <| by simp [rmap_sets, Set.preimage, SetRel.core_comp]

@[simp]
theorem rmap_compose (r : SetRel Œ± Œ≤) (s : SetRel Œ≤ Œ≥) : rmap s ‚àò rmap r = rmap (r.comp s) :=
  funext <| rmap_rmap _ _

/-- Generic "limit of a relation" predicate. `RTendsto r l‚ÇÅ l‚ÇÇ` asserts that for every
`l‚ÇÇ`-neighborhood `a`, the `r`-core of `a` is an `l‚ÇÅ`-neighborhood. One generalization of
`Filter.Tendsto` to relations. -/
def RTendsto (r : SetRel Œ± Œ≤) (l‚ÇÅ : Filter Œ±) (l‚ÇÇ : Filter Œ≤) :=
  l‚ÇÅ.rmap r ‚â§ l‚ÇÇ

theorem rtendsto_def (r : SetRel Œ± Œ≤) (l‚ÇÅ : Filter Œ±) (l‚ÇÇ : Filter Œ≤) :
    RTendsto r l‚ÇÅ l‚ÇÇ ‚Üî ‚àÄ s ‚àà l‚ÇÇ, r.core s ‚àà l‚ÇÅ :=
  Iff.rfl

/-- One way of taking the inverse map of a filter under a relation. One generalization of
`Filter.comap` to relations. Note that `Rel.core` generalizes `Set.preimage`. -/
def rcomap (r : SetRel Œ± Œ≤) (f : Filter Œ≤) : Filter Œ± where
  sets := SetRel.image {(s, t) : _ √ó _ | r.core s ‚äÜ t} f.sets
  univ_sets := ‚ü®Set.univ, univ_mem, Set.subset_univ _‚ü©
  sets_of_superset := fun ‚ü®a', ha', ma'a‚ü© ab => ‚ü®a', ha', ma'a.trans ab‚ü©
  inter_sets := fun ‚ü®a', ha‚ÇÅ, ha‚ÇÇ‚ü© ‚ü®b', hb‚ÇÅ, hb‚ÇÇ‚ü© =>
    ‚ü®a' ‚à© b', inter_mem ha‚ÇÅ hb‚ÇÅ, (r.core_inter a' b').subset.trans (Set.inter_subset_inter ha‚ÇÇ hb‚ÇÇ)‚ü©

theorem rcomap_sets (r : SetRel Œ± Œ≤) (f : Filter Œ≤) :
    (rcomap r f).sets = SetRel.image {(s, t) : _ √ó _ | r.core s ‚äÜ t} f.sets :=
  rfl

theorem rcomap_rcomap (r : SetRel Œ± Œ≤) (s : SetRel Œ≤ Œ≥) (l : Filter Œ≥) :
    rcomap r (rcomap s l) = rcomap (r.comp s) l :=
  filter_eq <| by
    ext t
    simp only [rcomap_sets, SetRel.image, Filter.mem_sets, Set.mem_setOf_eq, SetRel.core_comp]
    constructor
    ¬∑ rintro ‚ü®u, ‚ü®v, vsets, hv‚ü©, h‚ü©
      exact ‚ü®v, vsets, Set.Subset.trans (SetRel.core_mono hv) h‚ü©
    rintro ‚ü®t, tsets, ht‚ü©
    exact ‚ü®SetRel.core s t, ‚ü®t, tsets, Set.Subset.rfl‚ü©, ht‚ü©

@[simp]
theorem rcomap_compose (r : SetRel Œ± Œ≤) (s : SetRel Œ≤ Œ≥) :
    rcomap r ‚àò rcomap s = rcomap (r.comp s) :=
  funext <| rcomap_rcomap _ _

theorem rtendsto_iff_le_rcomap (r : SetRel Œ± Œ≤) (l‚ÇÅ : Filter Œ±) (l‚ÇÇ : Filter Œ≤) :
    RTendsto r l‚ÇÅ l‚ÇÇ ‚Üî l‚ÇÅ ‚â§ l‚ÇÇ.rcomap r := by
  rw [rtendsto_def]
  simp_rw [‚Üê l‚ÇÇ.mem_sets]
  constructor
  ¬∑ simpa [Filter.le_def, rcomap, SetRel.mem_image]
      using fun h s t tl‚ÇÇ => mem_of_superset (h t tl‚ÇÇ)
  ¬∑ simpa [Filter.le_def, rcomap, SetRel.mem_image]
      using fun h t tl‚ÇÇ => h _ t tl‚ÇÇ Set.Subset.rfl

-- Interestingly, there does not seem to be a way to express this relation using a forward map.
-- Given a filter `f` on `Œ±`, we want a filter `f'` on `Œ≤` such that `r.preimage s ‚àà f` if
-- and only if `s ‚àà f'`. But the intersection of two sets satisfying the lhs may be empty.
/-- One way of taking the inverse map of a filter under a relation. Generalization of `Filter.comap`
to relations. -/
def rcomap' (r : SetRel Œ± Œ≤) (f : Filter Œ≤) : Filter Œ± where
  sets := SetRel.image {(s, t) : _ √ó _ | r.preimage s ‚äÜ t} f.sets
  univ_sets := ‚ü®Set.univ, univ_mem, Set.subset_univ _‚ü©
  sets_of_superset := fun ‚ü®a', ha', ma'a‚ü© ab => ‚ü®a', ha', ma'a.trans ab‚ü©
  inter_sets := fun ‚ü®a', ha‚ÇÅ, ha‚ÇÇ‚ü© ‚ü®b', hb‚ÇÅ, hb‚ÇÇ‚ü© =>
    ‚ü®a' ‚à© b', inter_mem ha‚ÇÅ hb‚ÇÅ, r.preimage_inter_subset.trans (Set.inter_subset_inter ha‚ÇÇ hb‚ÇÇ)‚ü©

@[simp]
theorem mem_rcomap' (r : SetRel Œ± Œ≤) (l : Filter Œ≤) (s : Set Œ±) :
    s ‚àà l.rcomap' r ‚Üî ‚àÉ t ‚àà l, r.preimage t ‚äÜ s :=
  Iff.rfl

theorem rcomap'_sets (r : SetRel Œ± Œ≤) (f : Filter Œ≤) :
    (rcomap' r f).sets = SetRel.image {(s, t) | r.preimage s ‚äÜ t} f.sets :=
  rfl

@[simp]
theorem rcomap'_rcomap' (r : SetRel Œ± Œ≤) (s : SetRel Œ≤ Œ≥) (l : Filter Œ≥) :
    rcomap' r (rcomap' s l) = rcomap' (r.comp s) l :=
  Filter.ext fun t => by
    simp only [mem_rcomap', SetRel.preimage_comp]
    constructor
    ¬∑ rintro ‚ü®u, ‚ü®v, vsets, hv‚ü©, h‚ü©
      exact ‚ü®v, vsets, (SetRel.preimage_mono hv).trans h‚ü©
    rintro ‚ü®t, tsets, ht‚ü©
    exact ‚ü®s.preimage t, ‚ü®t, tsets, Set.Subset.rfl‚ü©, ht‚ü©

@[simp]
theorem rcomap'_compose (r : SetRel Œ± Œ≤) (s : SetRel Œ≤ Œ≥) :
    rcomap' r ‚àò rcomap' s = rcomap' (r.comp s) :=
  funext <| rcomap'_rcomap' _ _

/-- Generic "limit of a relation" predicate. `RTendsto' r l‚ÇÅ l‚ÇÇ` asserts that for every
`l‚ÇÇ`-neighborhood `a`, the `r`-preimage of `a` is an `l‚ÇÅ`-neighborhood. One generalization of
`Filter.Tendsto` to relations. -/
def RTendsto' (r : SetRel Œ± Œ≤) (l‚ÇÅ : Filter Œ±) (l‚ÇÇ : Filter Œ≤) :=
  l‚ÇÅ ‚â§ l‚ÇÇ.rcomap' r

theorem rtendsto'_def (r : SetRel Œ± Œ≤) (l‚ÇÅ : Filter Œ±) (l‚ÇÇ : Filter Œ≤) :
    RTendsto' r l‚ÇÅ l‚ÇÇ ‚Üî ‚àÄ s ‚àà l‚ÇÇ, r.preimage s ‚àà l‚ÇÅ := by
  unfold RTendsto' rcomap'; constructor
  ¬∑ simpa [le_def, SetRel.mem_image] using fun h s hs => h _ _ hs Set.Subset.rfl
  ¬∑ simpa [le_def, SetRel.mem_image] using fun h s t ht => mem_of_superset (h t ht)

theorem tendsto_iff_rtendsto (l‚ÇÅ : Filter Œ±) (l‚ÇÇ : Filter Œ≤) (f : Œ± ‚Üí Œ≤) :
    Tendsto f l‚ÇÅ l‚ÇÇ ‚Üî RTendsto (Function.graph f) l‚ÇÅ l‚ÇÇ := by
  simp [tendsto_def, Function.graph, rtendsto_def, SetRel.core, Set.preimage]

theorem tendsto_iff_rtendsto' (l‚ÇÅ : Filter Œ±) (l‚ÇÇ : Filter Œ≤) (f : Œ± ‚Üí Œ≤) :
    Tendsto f l‚ÇÅ l‚ÇÇ ‚Üî RTendsto' (Function.graph f) l‚ÇÅ l‚ÇÇ := by
  simp [tendsto_def, Function.graph, rtendsto'_def, SetRel.preimage, Set.preimage]

/-! ### Partial functions -/


/-- The forward map of a filter under a partial function. Generalization of `Filter.map` to partial
functions. -/
def pmap (f : Œ± ‚Üí. Œ≤) (l : Filter Œ±) : Filter Œ≤ :=
  Filter.rmap f.graph' l

@[simp]
theorem mem_pmap (f : Œ± ‚Üí. Œ≤) (l : Filter Œ±) (s : Set Œ≤) : s ‚àà l.pmap f ‚Üî f.core s ‚àà l :=
  Iff.rfl

/-- Generic "limit of a partial function" predicate. `PTendsto r l‚ÇÅ l‚ÇÇ` asserts that for every
`l‚ÇÇ`-neighborhood `a`, the `p`-core of `a` is an `l‚ÇÅ`-neighborhood. One generalization of
`Filter.Tendsto` to partial function. -/
def PTendsto (f : Œ± ‚Üí. Œ≤) (l‚ÇÅ : Filter Œ±) (l‚ÇÇ : Filter Œ≤) :=
  l‚ÇÅ.pmap f ‚â§ l‚ÇÇ

theorem ptendsto_def (f : Œ± ‚Üí. Œ≤) (l‚ÇÅ : Filter Œ±) (l‚ÇÇ : Filter Œ≤) :
    PTendsto f l‚ÇÅ l‚ÇÇ ‚Üî ‚àÄ s ‚àà l‚ÇÇ, f.core s ‚àà l‚ÇÅ :=
  Iff.rfl

theorem ptendsto_iff_rtendsto (l‚ÇÅ : Filter Œ±) (l‚ÇÇ : Filter Œ≤) (f : Œ± ‚Üí. Œ≤) :
    PTendsto f l‚ÇÅ l‚ÇÇ ‚Üî RTendsto f.graph' l‚ÇÅ l‚ÇÇ :=
  Iff.rfl

theorem pmap_res (l : Filter Œ±) (s : Set Œ±) (f : Œ± ‚Üí Œ≤) :
    pmap (PFun.res f s) l = map f (l ‚äì ùìü s) := by
  ext t
  simp only [PFun.core_res, mem_pmap, mem_map, mem_inf_principal, imp_iff_not_or]
  rfl

theorem tendsto_iff_ptendsto (l‚ÇÅ : Filter Œ±) (l‚ÇÇ : Filter Œ≤) (s : Set Œ±) (f : Œ± ‚Üí Œ≤) :
    Tendsto f (l‚ÇÅ ‚äì ùìü s) l‚ÇÇ ‚Üî PTendsto (PFun.res f s) l‚ÇÅ l‚ÇÇ := by
  simp only [Tendsto, PTendsto, pmap_res]

theorem tendsto_iff_ptendsto_univ (l‚ÇÅ : Filter Œ±) (l‚ÇÇ : Filter Œ≤) (f : Œ± ‚Üí Œ≤) :
    Tendsto f l‚ÇÅ l‚ÇÇ ‚Üî PTendsto (PFun.res f Set.univ) l‚ÇÅ l‚ÇÇ := by
  rw [‚Üê tendsto_iff_ptendsto]
  simp [principal_univ]

/-- Inverse map of a filter under a partial function. One generalization of `Filter.comap` to
partial functions. -/
def pcomap' (f : Œ± ‚Üí. Œ≤) (l : Filter Œ≤) : Filter Œ± :=
  Filter.rcomap' f.graph' l

/-- Generic "limit of a partial function" predicate. `PTendsto' r l‚ÇÅ l‚ÇÇ` asserts that for every
`l‚ÇÇ`-neighborhood `a`, the `p`-preimage of `a` is an `l‚ÇÅ`-neighborhood. One generalization of
`Filter.Tendsto` to partial functions. -/
def PTendsto' (f : Œ± ‚Üí. Œ≤) (l‚ÇÅ : Filter Œ±) (l‚ÇÇ : Filter Œ≤) :=
  l‚ÇÅ ‚â§ l‚ÇÇ.rcomap' f.graph'

theorem ptendsto'_def (f : Œ± ‚Üí. Œ≤) (l‚ÇÅ : Filter Œ±) (l‚ÇÇ : Filter Œ≤) :
    PTendsto' f l‚ÇÅ l‚ÇÇ ‚Üî ‚àÄ s ‚àà l‚ÇÇ, f.preimage s ‚àà l‚ÇÅ :=
  rtendsto'_def _ _ _

theorem ptendsto_of_ptendsto' {f : Œ± ‚Üí. Œ≤} {l‚ÇÅ : Filter Œ±} {l‚ÇÇ : Filter Œ≤} :
    PTendsto' f l‚ÇÅ l‚ÇÇ ‚Üí PTendsto f l‚ÇÅ l‚ÇÇ := by
  rw [ptendsto_def, ptendsto'_def]
  exact fun h s sl‚ÇÇ => mem_of_superset (h s sl‚ÇÇ) (PFun.preimage_subset_core _ _)

theorem ptendsto'_of_ptendsto {f : Œ± ‚Üí. Œ≤} {l‚ÇÅ : Filter Œ±} {l‚ÇÇ : Filter Œ≤} (h : f.Dom ‚àà l‚ÇÅ) :
    PTendsto f l‚ÇÅ l‚ÇÇ ‚Üí PTendsto' f l‚ÇÅ l‚ÇÇ := by
  rw [ptendsto_def, ptendsto'_def]
  intro h' s sl‚ÇÇ
  rw [PFun.preimage_eq]
  exact inter_mem (h' s sl‚ÇÇ) h

end Filter
