/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad
-/
import Mathlib.Order.Filter.Basic
import Mathlib.Data.PFun

#align_import order.filter.partial from "leanprover-community/mathlib"@"b363547b3113d350d053abdf2884e9850a56b205"

/-!
# `Tendsto` for relations and partial functions

This file generalizes `Filter` definitions from functions to partial functions and relations.

## Considering functions and partial functions as relations

A function `f : Î± â†’ Î²` can be considered as the relation `Rel Î± Î²` which relates `x` and `f x` for
all `x`, and nothing else. This relation is called `Function.Graph f`.

A partial function `f : Î± â†’. Î²` can be considered as the relation `Rel Î± Î²` which relates `x` and
`f x` for all `x` for which `f x` exists, and nothing else. This relation is called
`PFun.Graph' f`.

In this regard, a function is a relation for which every element in `Î±` is related to exactly one
element in `Î²` and a partial function is a relation for which every element in `Î±` is related to at
most one element in `Î²`.

This file leverages this analogy to generalize `Filter` definitions from functions to partial
functions and relations.

## Notes

`Set.preimage` can be generalized to relations in two ways:
* `Rel.preimage` returns the image of the set under the inverse relation.
* `Rel.core` returns the set of elements that are only related to those in the set.
Both generalizations are sensible in the context of filters, so `Filter.comap` and `Filter.Tendsto`
get two generalizations each.

We first take care of relations. Then the definitions for partial functions are taken as special
cases of the definitions for relations.
-/


universe u v w

namespace Filter

variable {Î± : Type u} {Î² : Type v} {Î³ : Type w}

open Filter

/-! ### Relations -/


/-- The forward map of a filter under a relation. Generalization of `Filter.map` to relations. Note
that `Rel.core` generalizes `Set.preimage`. -/
def rmap (r : Rel Î± Î²) (l : Filter Î±) : Filter Î² where
  sets := { s | r.core s âˆˆ l }
  univ_sets := by simp
                  -- ğŸ‰ no goals
  sets_of_superset hs st := mem_of_superset hs (Rel.core_mono _ st)
  inter_sets hs ht := by
    simp only [Set.mem_setOf_eq]
    -- âŠ¢ Rel.core r (xâœ âˆ© yâœ) âˆˆ l
    convert inter_mem hs ht
    -- âŠ¢ Rel.core r (xâœ âˆ© yâœ) = Rel.core r xâœ âˆ© Rel.core r yâœ
    rw [â†Rel.core_inter]
    -- ğŸ‰ no goals
#align filter.rmap Filter.rmap

theorem rmap_sets (r : Rel Î± Î²) (l : Filter Î±) : (l.rmap r).sets = r.core â»Â¹' l.sets :=
  rfl
#align filter.rmap_sets Filter.rmap_sets

@[simp]
theorem mem_rmap (r : Rel Î± Î²) (l : Filter Î±) (s : Set Î²) : s âˆˆ l.rmap r â†” r.core s âˆˆ l :=
  Iff.rfl
#align filter.mem_rmap Filter.mem_rmap

@[simp]
theorem rmap_rmap (r : Rel Î± Î²) (s : Rel Î² Î³) (l : Filter Î±) :
    rmap s (rmap r l) = rmap (r.comp s) l :=
  filter_eq <| by simp [rmap_sets, Set.preimage, Rel.core_comp]
                  -- ğŸ‰ no goals
#align filter.rmap_rmap Filter.rmap_rmap

@[simp]
theorem rmap_compose (r : Rel Î± Î²) (s : Rel Î² Î³) : rmap s âˆ˜ rmap r = rmap (r.comp s) :=
  funext <| rmap_rmap _ _
#align filter.rmap_compose Filter.rmap_compose

/-- Generic "limit of a relation" predicate. `RTendsto r lâ‚ lâ‚‚` asserts that for every
`lâ‚‚`-neighborhood `a`, the `r`-core of `a` is an `lâ‚`-neighborhood. One generalization of
`Filter.Tendsto` to relations. -/
def RTendsto (r : Rel Î± Î²) (lâ‚ : Filter Î±) (lâ‚‚ : Filter Î²) :=
  lâ‚.rmap r â‰¤ lâ‚‚
#align filter.rtendsto Filter.RTendsto

theorem rtendsto_def (r : Rel Î± Î²) (lâ‚ : Filter Î±) (lâ‚‚ : Filter Î²) :
    RTendsto r lâ‚ lâ‚‚ â†” âˆ€ s âˆˆ lâ‚‚, r.core s âˆˆ lâ‚ :=
  Iff.rfl
#align filter.rtendsto_def Filter.rtendsto_def

/-- One way of taking the inverse map of a filter under a relation. One generalization of
`Filter.comap` to relations. Note that `Rel.core` generalizes `Set.preimage`. -/
def rcomap (r : Rel Î± Î²) (f : Filter Î²) : Filter Î± where
  sets := Rel.image (fun s t => r.core s âŠ† t) f.sets
  univ_sets := âŸ¨Set.univ, univ_mem, Set.subset_univ _âŸ©
  sets_of_superset := fun âŸ¨a', ha', ma'aâŸ© ab => âŸ¨a', ha', ma'a.trans abâŸ©
  inter_sets := fun âŸ¨a', haâ‚, haâ‚‚âŸ© âŸ¨b', hbâ‚, hbâ‚‚âŸ© =>
    âŸ¨a' âˆ© b', inter_mem haâ‚ hbâ‚, (r.core_inter a' b').subset.trans (Set.inter_subset_inter haâ‚‚ hbâ‚‚)âŸ©
#align filter.rcomap Filter.rcomap

theorem rcomap_sets (r : Rel Î± Î²) (f : Filter Î²) :
    (rcomap r f).sets = Rel.image (fun s t => r.core s âŠ† t) f.sets :=
  rfl
#align filter.rcomap_sets Filter.rcomap_sets

theorem rcomap_rcomap (r : Rel Î± Î²) (s : Rel Î² Î³) (l : Filter Î³) :
    rcomap r (rcomap s l) = rcomap (r.comp s) l :=
  filter_eq <| by
    ext t; simp [rcomap_sets, Rel.image, Rel.core_comp]; constructor
    -- âŠ¢ t âˆˆ (rcomap r (rcomap s l)).sets â†” t âˆˆ (rcomap (Rel.comp r s) l).sets
           -- âŠ¢ (âˆƒ x, (âˆƒ x_1, x_1 âˆˆ l âˆ§ Rel.core s x_1 âŠ† x) âˆ§ Rel.core r x âŠ† t) â†” âˆƒ x, x âˆˆ l â€¦
                                                         -- âŠ¢ (âˆƒ x, (âˆƒ x_1, x_1 âˆˆ l âˆ§ Rel.core s x_1 âŠ† x) âˆ§ Rel.core r x âŠ† t) â†’ âˆƒ x, x âˆˆ l â€¦
    Â· rintro âŸ¨u, âŸ¨v, vsets, hvâŸ©, hâŸ©
      -- âŠ¢ âˆƒ x, x âˆˆ l âˆ§ Rel.core r (Rel.core s x) âŠ† t
      exact âŸ¨v, vsets, Set.Subset.trans (Rel.core_mono _ hv) hâŸ©
      -- ğŸ‰ no goals
    rintro âŸ¨t, tsets, htâŸ©
    -- âŠ¢ âˆƒ x, (âˆƒ x_1, x_1 âˆˆ l âˆ§ Rel.core s x_1 âŠ† x) âˆ§ Rel.core r x âŠ† tâœ
    exact âŸ¨Rel.core s t, âŸ¨t, tsets, Set.Subset.rflâŸ©, htâŸ©
    -- ğŸ‰ no goals
#align filter.rcomap_rcomap Filter.rcomap_rcomap

@[simp]
theorem rcomap_compose (r : Rel Î± Î²) (s : Rel Î² Î³) : rcomap r âˆ˜ rcomap s = rcomap (r.comp s) :=
  funext <| rcomap_rcomap _ _
#align filter.rcomap_compose Filter.rcomap_compose

theorem rtendsto_iff_le_rcomap (r : Rel Î± Î²) (lâ‚ : Filter Î±) (lâ‚‚ : Filter Î²) :
    RTendsto r lâ‚ lâ‚‚ â†” lâ‚ â‰¤ lâ‚‚.rcomap r := by
  rw [rtendsto_def]
  -- âŠ¢ (âˆ€ (s : Set Î²), s âˆˆ lâ‚‚ â†’ Rel.core r s âˆˆ lâ‚) â†” lâ‚ â‰¤ rcomap r lâ‚‚
  simp_rw [â†lâ‚‚.mem_sets]
  -- âŠ¢ (âˆ€ (s : Set Î²), s âˆˆ lâ‚‚.sets â†’ Rel.core r s âˆˆ lâ‚) â†” lâ‚ â‰¤ rcomap r lâ‚‚
  simp [Filter.le_def, rcomap, Rel.mem_image]; constructor
  -- âŠ¢ (âˆ€ (s : Set Î²), s âˆˆ lâ‚‚ â†’ Rel.core r s âˆˆ lâ‚) â†” âˆ€ (x : Set Î±) (x_1 : Set Î²), x â€¦
                                               -- âŠ¢ (âˆ€ (s : Set Î²), s âˆˆ lâ‚‚ â†’ Rel.core r s âˆˆ lâ‚) â†’ âˆ€ (x : Set Î±) (x_1 : Set Î²), x â€¦
  Â· exact fun h s t tlâ‚‚ => mem_of_superset (h t tlâ‚‚)
    -- ğŸ‰ no goals
  Â· exact fun h t tlâ‚‚ => h _ t tlâ‚‚ Set.Subset.rfl
    -- ğŸ‰ no goals
#align filter.rtendsto_iff_le_rcomap Filter.rtendsto_iff_le_rcomap

-- Interestingly, there does not seem to be a way to express this relation using a forward map.
-- Given a filter `f` on `Î±`, we want a filter `f'` on `Î²` such that `r.preimage s âˆˆ f` if
-- and only if `s âˆˆ f'`. But the intersection of two sets satisfying the lhs may be empty.
/-- One way of taking the inverse map of a filter under a relation. Generalization of `Filter.comap`
to relations. -/
def rcomap' (r : Rel Î± Î²) (f : Filter Î²) : Filter Î± where
  sets := Rel.image (fun s t => r.preimage s âŠ† t) f.sets
  univ_sets := âŸ¨Set.univ, univ_mem, Set.subset_univ _âŸ©
  sets_of_superset := fun âŸ¨a', ha', ma'aâŸ© ab => âŸ¨a', ha', ma'a.trans abâŸ©
  inter_sets := fun âŸ¨a', haâ‚, haâ‚‚âŸ© âŸ¨b', hbâ‚, hbâ‚‚âŸ© =>
    âŸ¨a' âˆ© b', inter_mem haâ‚ hbâ‚,
      (@Rel.preimage_inter _ _ r _ _).trans (Set.inter_subset_inter haâ‚‚ hbâ‚‚)âŸ©
#align filter.rcomap' Filter.rcomap'

@[simp]
theorem mem_rcomap' (r : Rel Î± Î²) (l : Filter Î²) (s : Set Î±) :
    s âˆˆ l.rcomap' r â†” âˆƒ t âˆˆ l, r.preimage t âŠ† s :=
  Iff.rfl
#align filter.mem_rcomap' Filter.mem_rcomap'

theorem rcomap'_sets (r : Rel Î± Î²) (f : Filter Î²) :
    (rcomap' r f).sets = Rel.image (fun s t => r.preimage s âŠ† t) f.sets :=
  rfl
#align filter.rcomap'_sets Filter.rcomap'_sets

@[simp]
theorem rcomap'_rcomap' (r : Rel Î± Î²) (s : Rel Î² Î³) (l : Filter Î³) :
    rcomap' r (rcomap' s l) = rcomap' (r.comp s) l :=
  Filter.ext fun t => by
    simp only [mem_rcomap', Rel.preimage_comp]
    -- âŠ¢ (âˆƒ t_1, (âˆƒ t, t âˆˆ l âˆ§ Rel.preimage s t âŠ† t_1) âˆ§ Rel.preimage r t_1 âŠ† t) â†” âˆƒ  â€¦
    constructor
    -- âŠ¢ (âˆƒ t_1, (âˆƒ t, t âˆˆ l âˆ§ Rel.preimage s t âŠ† t_1) âˆ§ Rel.preimage r t_1 âŠ† t) â†’ âˆƒ  â€¦
    Â· rintro âŸ¨u, âŸ¨v, vsets, hvâŸ©, hâŸ©
      -- âŠ¢ âˆƒ t_1, t_1 âˆˆ l âˆ§ Rel.preimage r (Rel.preimage s t_1) âŠ† t
      exact âŸ¨v, vsets, (Rel.preimage_mono _ hv).trans hâŸ©
      -- ğŸ‰ no goals
    rintro âŸ¨t, tsets, htâŸ©
    -- âŠ¢ âˆƒ t, (âˆƒ t_1, t_1 âˆˆ l âˆ§ Rel.preimage s t_1 âŠ† t) âˆ§ Rel.preimage r t âŠ† tâœ
    exact âŸ¨s.preimage t, âŸ¨t, tsets, Set.Subset.rflâŸ©, htâŸ©
    -- ğŸ‰ no goals
#align filter.rcomap'_rcomap' Filter.rcomap'_rcomap'

@[simp]
theorem rcomap'_compose (r : Rel Î± Î²) (s : Rel Î² Î³) : rcomap' r âˆ˜ rcomap' s = rcomap' (r.comp s) :=
  funext <| rcomap'_rcomap' _ _
#align filter.rcomap'_compose Filter.rcomap'_compose

/-- Generic "limit of a relation" predicate. `RTendsto' r lâ‚ lâ‚‚` asserts that for every
`lâ‚‚`-neighborhood `a`, the `r`-preimage of `a` is an `lâ‚`-neighborhood. One generalization of
`Filter.Tendsto` to relations. -/
def RTendsto' (r : Rel Î± Î²) (lâ‚ : Filter Î±) (lâ‚‚ : Filter Î²) :=
  lâ‚ â‰¤ lâ‚‚.rcomap' r
#align filter.rtendsto' Filter.RTendsto'

theorem rtendsto'_def (r : Rel Î± Î²) (lâ‚ : Filter Î±) (lâ‚‚ : Filter Î²) :
    RTendsto' r lâ‚ lâ‚‚ â†” âˆ€ s âˆˆ lâ‚‚, r.preimage s âˆˆ lâ‚ := by
  unfold RTendsto' rcomap'; simp [le_def, Rel.mem_image]; constructor
  -- âŠ¢ lâ‚ â‰¤ { sets := Rel.image (fun s t => Rel.preimage r s âŠ† t) lâ‚‚.sets, univ_set â€¦
                            -- âŠ¢ (âˆ€ (x : Set Î±) (x_1 : Set Î²), x_1 âˆˆ lâ‚‚ â†’ Rel.preimage r x_1 âŠ† x â†’ x âˆˆ lâ‚) â†”  â€¦
                                                          -- âŠ¢ (âˆ€ (x : Set Î±) (x_1 : Set Î²), x_1 âˆˆ lâ‚‚ â†’ Rel.preimage r x_1 âŠ† x â†’ x âˆˆ lâ‚) â†’  â€¦
  Â· exact fun h s hs => h _ _ hs Set.Subset.rfl
    -- ğŸ‰ no goals
  Â· exact fun h s t ht => mem_of_superset (h t ht)
    -- ğŸ‰ no goals
#align filter.rtendsto'_def Filter.rtendsto'_def

theorem tendsto_iff_rtendsto (lâ‚ : Filter Î±) (lâ‚‚ : Filter Î²) (f : Î± â†’ Î²) :
    Tendsto f lâ‚ lâ‚‚ â†” RTendsto (Function.graph f) lâ‚ lâ‚‚ := by
  simp [tendsto_def, Function.graph, rtendsto_def, Rel.core, Set.preimage]
  -- ğŸ‰ no goals
#align filter.tendsto_iff_rtendsto Filter.tendsto_iff_rtendsto

theorem tendsto_iff_rtendsto' (lâ‚ : Filter Î±) (lâ‚‚ : Filter Î²) (f : Î± â†’ Î²) :
    Tendsto f lâ‚ lâ‚‚ â†” RTendsto' (Function.graph f) lâ‚ lâ‚‚ := by
  simp [tendsto_def, Function.graph, rtendsto'_def, Rel.preimage_def, Set.preimage]
  -- ğŸ‰ no goals
#align filter.tendsto_iff_rtendsto' Filter.tendsto_iff_rtendsto'

/-! ### Partial functions -/


/-- The forward map of a filter under a partial function. Generalization of `Filter.map` to partial
functions. -/
def pmap (f : Î± â†’. Î²) (l : Filter Î±) : Filter Î² :=
  Filter.rmap f.graph' l
#align filter.pmap Filter.pmap

@[simp]
theorem mem_pmap (f : Î± â†’. Î²) (l : Filter Î±) (s : Set Î²) : s âˆˆ l.pmap f â†” f.core s âˆˆ l :=
  Iff.rfl
#align filter.mem_pmap Filter.mem_pmap

/-- Generic "limit of a partial function" predicate. `PTendsto r lâ‚ lâ‚‚` asserts that for every
`lâ‚‚`-neighborhood `a`, the `p`-core of `a` is an `lâ‚`-neighborhood. One generalization of
`Filter.Tendsto` to partial function. -/
def PTendsto (f : Î± â†’. Î²) (lâ‚ : Filter Î±) (lâ‚‚ : Filter Î²) :=
  lâ‚.pmap f â‰¤ lâ‚‚
#align filter.ptendsto Filter.PTendsto

theorem ptendsto_def (f : Î± â†’. Î²) (lâ‚ : Filter Î±) (lâ‚‚ : Filter Î²) :
    PTendsto f lâ‚ lâ‚‚ â†” âˆ€ s âˆˆ lâ‚‚, f.core s âˆˆ lâ‚ :=
  Iff.rfl
#align filter.ptendsto_def Filter.ptendsto_def

theorem ptendsto_iff_rtendsto (lâ‚ : Filter Î±) (lâ‚‚ : Filter Î²) (f : Î± â†’. Î²) :
    PTendsto f lâ‚ lâ‚‚ â†” RTendsto f.graph' lâ‚ lâ‚‚ :=
  Iff.rfl
#align filter.ptendsto_iff_rtendsto Filter.ptendsto_iff_rtendsto

theorem pmap_res (l : Filter Î±) (s : Set Î±) (f : Î± â†’ Î²) :
    pmap (PFun.res f s) l = map f (l âŠ“ ğ“Ÿ s) := by
  ext t
  -- âŠ¢ t âˆˆ pmap (PFun.res f s) l â†” t âˆˆ map f (l âŠ“ ğ“Ÿ s)
  simp only [PFun.core_res, mem_pmap, mem_map, mem_inf_principal, imp_iff_not_or]
  -- âŠ¢ sá¶œ âˆª f â»Â¹' t âˆˆ l â†” {x | Â¬x âˆˆ s âˆ¨ x âˆˆ f â»Â¹' t} âˆˆ l
  rfl
  -- ğŸ‰ no goals
#align filter.pmap_res Filter.pmap_res

theorem tendsto_iff_ptendsto (lâ‚ : Filter Î±) (lâ‚‚ : Filter Î²) (s : Set Î±) (f : Î± â†’ Î²) :
    Tendsto f (lâ‚ âŠ“ ğ“Ÿ s) lâ‚‚ â†” PTendsto (PFun.res f s) lâ‚ lâ‚‚ := by
  simp only [Tendsto, PTendsto, pmap_res]
  -- ğŸ‰ no goals
#align filter.tendsto_iff_ptendsto Filter.tendsto_iff_ptendsto

theorem tendsto_iff_ptendsto_univ (lâ‚ : Filter Î±) (lâ‚‚ : Filter Î²) (f : Î± â†’ Î²) :
    Tendsto f lâ‚ lâ‚‚ â†” PTendsto (PFun.res f Set.univ) lâ‚ lâ‚‚ := by
  rw [â† tendsto_iff_ptendsto]
  -- âŠ¢ Tendsto f lâ‚ lâ‚‚ â†” Tendsto f (lâ‚ âŠ“ ğ“Ÿ Set.univ) lâ‚‚
  simp [principal_univ]
  -- ğŸ‰ no goals
#align filter.tendsto_iff_ptendsto_univ Filter.tendsto_iff_ptendsto_univ

/-- Inverse map of a filter under a partial function. One generalization of `Filter.comap` to
partial functions. -/
def pcomap' (f : Î± â†’. Î²) (l : Filter Î²) : Filter Î± :=
  Filter.rcomap' f.graph' l
#align filter.pcomap' Filter.pcomap'

/-- Generic "limit of a partial function" predicate. `PTendsto' r lâ‚ lâ‚‚` asserts that for every
`lâ‚‚`-neighborhood `a`, the `p`-preimage of `a` is an `lâ‚`-neighborhood. One generalization of
`Filter.Tendsto` to partial functions. -/
def PTendsto' (f : Î± â†’. Î²) (lâ‚ : Filter Î±) (lâ‚‚ : Filter Î²) :=
  lâ‚ â‰¤ lâ‚‚.rcomap' f.graph'
#align filter.ptendsto' Filter.PTendsto'

theorem ptendsto'_def (f : Î± â†’. Î²) (lâ‚ : Filter Î±) (lâ‚‚ : Filter Î²) :
    PTendsto' f lâ‚ lâ‚‚ â†” âˆ€ s âˆˆ lâ‚‚, f.preimage s âˆˆ lâ‚ :=
  rtendsto'_def _ _ _
#align filter.ptendsto'_def Filter.ptendsto'_def

theorem ptendsto_of_ptendsto' {f : Î± â†’. Î²} {lâ‚ : Filter Î±} {lâ‚‚ : Filter Î²} :
    PTendsto' f lâ‚ lâ‚‚ â†’ PTendsto f lâ‚ lâ‚‚ := by
  rw [ptendsto_def, ptendsto'_def]
  -- âŠ¢ (âˆ€ (s : Set Î²), s âˆˆ lâ‚‚ â†’ PFun.preimage f s âˆˆ lâ‚) â†’ âˆ€ (s : Set Î²), s âˆˆ lâ‚‚ â†’ P â€¦
  exact fun h s slâ‚‚ => mem_of_superset (h s slâ‚‚) (PFun.preimage_subset_core _ _)
  -- ğŸ‰ no goals
#align filter.ptendsto_of_ptendsto' Filter.ptendsto_of_ptendsto'

theorem ptendsto'_of_ptendsto {f : Î± â†’. Î²} {lâ‚ : Filter Î±} {lâ‚‚ : Filter Î²} (h : f.Dom âˆˆ lâ‚) :
    PTendsto f lâ‚ lâ‚‚ â†’ PTendsto' f lâ‚ lâ‚‚ := by
  rw [ptendsto_def, ptendsto'_def]
  -- âŠ¢ (âˆ€ (s : Set Î²), s âˆˆ lâ‚‚ â†’ PFun.core f s âˆˆ lâ‚) â†’ âˆ€ (s : Set Î²), s âˆˆ lâ‚‚ â†’ PFun. â€¦
  intro h' s slâ‚‚
  -- âŠ¢ PFun.preimage f s âˆˆ lâ‚
  rw [PFun.preimage_eq]
  -- âŠ¢ PFun.core f s âˆ© PFun.Dom f âˆˆ lâ‚
  exact inter_mem (h' s slâ‚‚) h
  -- ğŸ‰ no goals
#align filter.ptendsto'_of_ptendsto Filter.ptendsto'_of_ptendsto

end Filter
