/-
Copyright (c) 2018 Simon Hudon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Simon Hudon
-/
import Mathlib.Control.Applicative
import Mathlib.Control.Traversable.Basic

#align_import control.traversable.lemmas from "leanprover-community/mathlib"@"3342d1b2178381196f818146ff79bc0e7ccd9e2d"

/-!
# Traversing collections

This file proves basic properties of traversable and applicative functors and defines
`PureTransformation F`, the natural applicative transformation from the identity functor to `F`.

## References

Inspired by [The Essence of the Iterator Pattern][gibbons2009].
-/


universe u

open LawfulTraversable

open Function hiding comp

open Functor

attribute [functor_norm] LawfulTraversable.naturality

attribute [simp] LawfulTraversable.id_traverse

namespace Traversable

variable {t : Type u â†’ Type u}

variable [Traversable t] [LawfulTraversable t]

variable (F G : Type u â†’ Type u)

variable [Applicative F] [LawfulApplicative F]

variable [Applicative G] [LawfulApplicative G]

variable {Î± Î² Î³ : Type u}

variable (g : Î± â†’ F Î²)

variable (h : Î² â†’ G Î³)

variable (f : Î² â†’ Î³)

/-- The natural applicative transformation from the identity functor
to `F`, defined by `pure : Î  {Î±}, Î± â†’ F Î±`. -/
def PureTransformation :
    ApplicativeTransformation Id F where
  app := @pure F _
  preserves_pure' x := rfl
  preserves_seq' f x := by
    simp only [map_pure, seq_pure]
    -- âŠ¢ pure (Seq.seq f fun x_1 => x) = pure (f x)
    rfl
    -- ğŸ‰ no goals
#align traversable.pure_transformation Traversable.PureTransformation

@[simp]
theorem pureTransformation_apply {Î±} (x : id Î±) : PureTransformation F x = pure x :=
  rfl
#align traversable.pure_transformation_apply Traversable.pureTransformation_apply

variable {F G} (x : t Î²)

-- Porting note: need to specify `m/F/G := Id` because `id` no longer has a `Monad` instance
theorem map_eq_traverse_id : map (f := t) f = traverse (m := Id) (pure âˆ˜ f) :=
  funext fun y => (traverse_eq_map_id f y).symm
#align traversable.map_eq_traverse_id Traversable.map_eq_traverse_id

theorem map_traverse (x : t Î±) : map f <$> traverse g x = traverse (map f âˆ˜ g) x := by
  rw [map_eq_traverse_id f]
  -- âŠ¢ traverse (pure âˆ˜ f) <$> traverse g x = traverse (map f âˆ˜ g) x
  refine' (comp_traverse (pure âˆ˜ f) g x).symm.trans _
  -- âŠ¢ traverse (Comp.mk âˆ˜ map (pure âˆ˜ f) âˆ˜ g) x = traverse (map f âˆ˜ g) x
  congr; apply Comp.applicative_comp_id
  -- âŠ¢ Comp.instApplicativeComp = instâœÂ³
         -- ğŸ‰ no goals
#align traversable.map_traverse Traversable.map_traverse

theorem traverse_map (f : Î² â†’ F Î³) (g : Î± â†’ Î²) (x : t Î±) :
    traverse f (g <$> x) = traverse (f âˆ˜ g) x := by
  rw [@map_eq_traverse_id t _ _ _ _ g]
  -- âŠ¢ traverse f (traverse (pure âˆ˜ g) x) = traverse (f âˆ˜ g) x
  refine' (comp_traverse (G := Id) f (pure âˆ˜ g) x).symm.trans _
  -- âŠ¢ traverse (Comp.mk âˆ˜ map f âˆ˜ pure âˆ˜ g) x = traverse (f âˆ˜ g) x
  congr; apply Comp.applicative_id_comp
  -- âŠ¢ Comp.instApplicativeComp = instâœÂ³
         -- ğŸ‰ no goals
#align traversable.traverse_map Traversable.traverse_map

theorem pure_traverse (x : t Î±) : traverse pure x = (pure x : F (t Î±)) := by
  have : traverse pure x = pure (traverse (m := Id) pure x) :=
      (naturality (PureTransformation F) pure x).symm
  rwa [id_traverse] at this
  -- ğŸ‰ no goals
#align traversable.pure_traverse Traversable.pure_traverse

theorem id_sequence (x : t Î±) : sequence (f := Id) (pure <$> x) = pure x := by
  simp [sequence, traverse_map, id_traverse]
  -- ğŸ‰ no goals
#align traversable.id_sequence Traversable.id_sequence

theorem comp_sequence (x : t (F (G Î±))) :
    sequence (Comp.mk <$> x) = Comp.mk (sequence <$> sequence x) := by
  simp [sequence, traverse_map]; rw [â† comp_traverse]; simp [map_id]
  -- âŠ¢ traverse Comp.mk x = Comp.mk (traverse id <$> traverse id x)
                                 -- âŠ¢ traverse Comp.mk x = traverse (Comp.mk âˆ˜ map id âˆ˜ id) x
                                                       -- ğŸ‰ no goals
#align traversable.comp_sequence Traversable.comp_sequence

theorem naturality' (Î· : ApplicativeTransformation F G) (x : t (F Î±)) :
    Î· (sequence x) = sequence (@Î· _ <$> x) := by simp [sequence, naturality, traverse_map]
                                                 -- ğŸ‰ no goals
#align traversable.naturality' Traversable.naturality'

@[functor_norm]
theorem traverse_id : traverse pure = (pure : t Î± â†’ Id (t Î±)) := by
  ext
  -- âŠ¢ traverse pure xâœ = pure xâœ
  exact id_traverse _
  -- ğŸ‰ no goals
#align traversable.traverse_id Traversable.traverse_id

@[functor_norm]
theorem traverse_comp (g : Î± â†’ F Î²) (h : Î² â†’ G Î³) :
    traverse (Comp.mk âˆ˜ map h âˆ˜ g) =
      (Comp.mk âˆ˜ map (traverse h) âˆ˜ traverse g : t Î± â†’ Comp F G (t Î³)) := by
  ext
  -- âŠ¢ traverse (Comp.mk âˆ˜ map h âˆ˜ g) xâœ = (Comp.mk âˆ˜ map (traverse h) âˆ˜ traverse g â€¦
  exact comp_traverse _ _ _
  -- ğŸ‰ no goals
#align traversable.traverse_comp Traversable.traverse_comp

theorem traverse_eq_map_id' (f : Î² â†’ Î³) :
  traverse (m := Id) (pure âˆ˜ f) = pure âˆ˜ (map f : t Î² â†’ t Î³) := by
  ext
  -- âŠ¢ traverse (pure âˆ˜ f) xâœ = (pure âˆ˜ map f) xâœ
  exact traverse_eq_map_id _ _
  -- ğŸ‰ no goals
#align traversable.traverse_eq_map_id' Traversable.traverse_eq_map_id'

-- @[functor_norm]
theorem traverse_map' (g : Î± â†’ Î²) (h : Î² â†’ G Î³) :
    traverse (h âˆ˜ g) = (traverse h âˆ˜ map g : t Î± â†’ G (t Î³)) := by
  ext
  -- âŠ¢ traverse (h âˆ˜ g) xâœ = (traverse h âˆ˜ map g) xâœ
  rw [comp_apply, traverse_map]
  -- ğŸ‰ no goals
#align traversable.traverse_map' Traversable.traverse_map'

theorem map_traverse' (g : Î± â†’ G Î²) (h : Î² â†’ Î³) :
    traverse (map h âˆ˜ g) = (map (map h) âˆ˜ traverse g : t Î± â†’ G (t Î³)) := by
  ext
  -- âŠ¢ traverse (map h âˆ˜ g) xâœ = (map (map h) âˆ˜ traverse g) xâœ
  rw [comp_apply, map_traverse]
  -- ğŸ‰ no goals
#align traversable.map_traverse' Traversable.map_traverse'

theorem naturality_pf (Î· : ApplicativeTransformation F G) (f : Î± â†’ F Î²) :
    traverse (@Î· _ âˆ˜ f) = @Î· _ âˆ˜ (traverse f : t Î± â†’ F (t Î²)) := by
  ext
  -- âŠ¢ traverse ((fun {Î±} => ApplicativeTransformation.app Î· Î±) âˆ˜ f) xâœ = ((fun {Î±} â€¦
  rw [comp_apply, naturality]
  -- ğŸ‰ no goals
#align traversable.naturality_pf Traversable.naturality_pf

end Traversable
