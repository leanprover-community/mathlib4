/-
Copyright (c) 2017 Simon Hudon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Simon Hudon
-/
import Mathlib.Control.Basic
import Mathlib.Init.Set
import Mathlib.Tactic.Basic
import Std.Tactic.Lint

#align_import control.functor from "leanprover-community/mathlib"@"70d50ecfd4900dd6d328da39ab7ebd516abe4025"

/-!
# Functors

This module provides additional lemmas, definitions, and instances for `Functor`s.

## Main definitions

* `Functor.Const Œ±` is the functor that sends all types to `Œ±`.
* `Functor.AddConst Œ±` is `Functor.Const Œ±` but for when `Œ±` has an additive structure.
* `Functor.Comp F G` for functors `F` and `G` is the functor composition of `F` and `G`.
* `Liftp` and `Liftr` respectively lift predicates and relations on a type `Œ±`
  to `F Œ±`.  Terms of `F Œ±` are considered to, in some sense, contain values of type `Œ±`.

## Tags

functor, applicative
-/

universe u v w

section Functor

variable {F : Type u ‚Üí Type v}

variable {Œ± Œ≤ Œ≥ : Type u}

variable [Functor F] [LawfulFunctor F]

theorem Functor.map_id : (¬∑ <$> ¬∑) id = (id : F Œ± ‚Üí F Œ±) := funext id_map
#align functor.map_id Functor.map_id

theorem Functor.map_comp_map (f : Œ± ‚Üí Œ≤) (g : Œ≤ ‚Üí Œ≥) :
    ((¬∑ <$> ¬∑) g ‚àò (¬∑ <$> ¬∑) f : F Œ± ‚Üí F Œ≥) = (¬∑ <$> ¬∑) (g ‚àò f) :=
  funext <| fun _ => (comp_map _ _ _).symm
  -- porting note: was `apply funext <;> intro <;> rw [comp_map]` but `rw` failed?
#align functor.map_comp_map Functor.map_comp_map

theorem Functor.ext {F} :
    ‚àÄ {F1 : Functor F} {F2 : Functor F} [@LawfulFunctor F F1] [@LawfulFunctor F F2],
    (‚àÄ (Œ± Œ≤) (f : Œ± ‚Üí Œ≤) (x : F Œ±), @Functor.map _ F1 _ _ f x = @Functor.map _ F2 _ _ f x) ‚Üí
    F1 = F2
  | ‚ü®m, mc‚ü©, ‚ü®m', mc'‚ü©, H1, H2, H => by
    cases show @m = @m' by funext Œ± Œ≤ f x; apply H
    -- ‚ä¢ { map := m, mapConst := mc } = { map := m, mapConst := mc' }
    congr
    -- ‚ä¢ mc = mc'
    funext Œ± Œ≤
    -- ‚ä¢ mc = mc'
    have E1 := @map_const _ ‚ü®@m, @mc‚ü© H1
    -- ‚ä¢ mc = mc'
    have E2 := @map_const _ ‚ü®@m, @mc'‚ü© H2
    -- ‚ä¢ mc = mc'
    exact E1.trans E2.symm
    -- üéâ no goals
#align functor.ext Functor.ext

end Functor

/-- Introduce `id` as a quasi-functor. (Note that where a lawful `Monad` or
`Applicative` or `Functor` is needed, `Id` is the correct definition). -/
def id.mk {Œ± : Sort u} : Œ± ‚Üí id Œ± :=
  id
#align id.mk id.mk

namespace Functor

/-- `Const Œ±` is the constant functor, mapping every type to `Œ±`. When
`Œ±` has a monoid structure, `Const Œ±` has an `Applicative` instance.
(If `Œ±` has an additive monoid structure, see `Functor.AddConst`.) -/
@[nolint unusedArguments]
def Const (Œ± : Type*) (_Œ≤ : Type*) :=
  Œ±
#align functor.const Functor.Const

/-- `Const.mk` is the canonical map `Œ± ‚Üí Const Œ± Œ≤` (the identity), and
it can be used as a pattern to extract this value. -/
@[match_pattern]
def Const.mk {Œ± Œ≤} (x : Œ±) : Const Œ± Œ≤ :=
  x
#align functor.const.mk Functor.Const.mk

/-- `Const.mk'` is `Const.mk` but specialized to map `Œ±` to
`Const Œ± PUnit`, where `PUnit` is the terminal object in `Type*`. -/
def Const.mk' {Œ±} (x : Œ±) : Const Œ± PUnit :=
  x
#align functor.const.mk' Functor.Const.mk'

/-- Extract the element of `Œ±` from the `Const` functor. -/
def Const.run {Œ± Œ≤} (x : Const Œ± Œ≤) : Œ± :=
  x
#align functor.const.run Functor.Const.run

namespace Const

protected theorem ext {Œ± Œ≤} {x y : Const Œ± Œ≤} (h : x.run = y.run) : x = y :=
  h
#align functor.const.ext Functor.Const.ext

/-- The map operation of the `Const Œ≥` functor. -/
@[nolint unusedArguments]
protected def map {Œ≥ Œ± Œ≤} (_f : Œ± ‚Üí Œ≤) (x : Const Œ≥ Œ≤) : Const Œ≥ Œ± :=
  x
#align functor.const.map Functor.Const.map

instance functor {Œ≥} : Functor (Const Œ≥) where map := @Const.map Œ≥

instance lawfulFunctor {Œ≥} : LawfulFunctor (Const Œ≥) := by constructor <;> intros <;> rfl
                                                                           -- ‚ä¢ mapConst = map ‚àò Function.const Œ≤‚úù
                                                                           -- ‚ä¢ id <$> x‚úù = x‚úù
                                                                           -- ‚ä¢ (h‚úù ‚àò g‚úù) <$> x‚úù = h‚úù <$> g‚úù <$> x‚úù
                                                                                      -- üéâ no goals
                                                                                      -- üéâ no goals
                                                                                      -- üéâ no goals

instance {Œ± Œ≤} [Inhabited Œ±] : Inhabited (Const Œ± Œ≤) :=
  ‚ü®(default : Œ±)‚ü©

end Const

/-- `AddConst Œ±` is a synonym for constant functor `Const Œ±`, mapping
every type to `Œ±`. When `Œ±` has an additive monoid structure,
`AddConst Œ±` has an `Applicative` instance. (If `Œ±` has a
multiplicative monoid structure, see `Functor.Const`.) -/
def AddConst (Œ± : Type*) :=
  Const Œ±
#align functor.add_const Functor.AddConst

/-- `AddConst.mk` is the canonical map `Œ± ‚Üí AddConst Œ± Œ≤`, which is the identity,
where `AddConst Œ± Œ≤ = Const Œ± Œ≤`. It can be used as a pattern to extract this value. -/
@[match_pattern]
def AddConst.mk {Œ± Œ≤} (x : Œ±) : AddConst Œ± Œ≤ :=
  x
#align functor.add_const.mk Functor.AddConst.mk

/-- Extract the element of `Œ±` from the constant functor. -/
def AddConst.run {Œ± Œ≤} : AddConst Œ± Œ≤ ‚Üí Œ± :=
  id
#align functor.add_const.run Functor.AddConst.run

instance AddConst.functor {Œ≥} : Functor (AddConst Œ≥) :=
  @Const.functor Œ≥
#align functor.add_const.functor Functor.AddConst.functor

instance AddConst.lawfulFunctor {Œ≥} : LawfulFunctor (AddConst Œ≥) :=
  @Const.lawfulFunctor Œ≥
#align functor.add_const.is_lawful_functor Functor.AddConst.lawfulFunctor

instance {Œ± Œ≤} [Inhabited Œ±] : Inhabited (AddConst Œ± Œ≤) :=
  ‚ü®(default : Œ±)‚ü©

/-- `Functor.Comp` is a wrapper around `Function.Comp` for types.
    It prevents Lean's type class resolution mechanism from trying
    a `Functor (Comp F id)` when `Functor F` would do. -/
def Comp (F : Type u ‚Üí Type w) (G : Type v ‚Üí Type u) (Œ± : Type v) : Type w :=
  F <| G Œ±
#align functor.comp Functor.Comp

/-- Construct a term of `Comp F G Œ±` from a term of `F (G Œ±)`, which is the same type.
Can be used as a pattern to extract a term of `F (G Œ±)`. -/
@[match_pattern]
def Comp.mk {F : Type u ‚Üí Type w} {G : Type v ‚Üí Type u} {Œ± : Type v} (x : F (G Œ±)) : Comp F G Œ± :=
  x
#align functor.comp.mk Functor.Comp.mk

/-- Extract a term of `F (G Œ±)` from a term of `Comp F G Œ±`, which is the same type. -/
def Comp.run {F : Type u ‚Üí Type w} {G : Type v ‚Üí Type u} {Œ± : Type v} (x : Comp F G Œ±) : F (G Œ±) :=
  x
#align functor.comp.run Functor.Comp.run

namespace Comp

variable {F : Type u ‚Üí Type w} {G : Type v ‚Üí Type u}

protected theorem ext {Œ±} {x y : Comp F G Œ±} : x.run = y.run ‚Üí x = y :=
  id
#align functor.comp.ext Functor.Comp.ext

instance {Œ±} [Inhabited (F (G Œ±))] : Inhabited (Comp F G Œ±) :=
  ‚ü®(default : F (G Œ±))‚ü©

variable [Functor F] [Functor G]

/-- The map operation for the composition `Comp F G` of functors `F` and `G`. -/
protected def map {Œ± Œ≤ : Type v} (h : Œ± ‚Üí Œ≤) : Comp F G Œ± ‚Üí Comp F G Œ≤
  | Comp.mk x => Comp.mk ((¬∑ <$> ¬∑) h <$> x)
#align functor.comp.map Functor.Comp.map

instance functor : Functor (Comp F G) where map := @Comp.map F G _ _

@[functor_norm]
theorem map_mk {Œ± Œ≤} (h : Œ± ‚Üí Œ≤) (x : F (G Œ±)) : h <$> Comp.mk x = Comp.mk ((¬∑ <$> ¬∑) h <$> x) :=
  rfl
#align functor.comp.map_mk Functor.Comp.map_mk

@[simp]
protected theorem run_map {Œ± Œ≤} (h : Œ± ‚Üí Œ≤) (x : Comp F G Œ±) :
    (h <$> x).run = (¬∑ <$> ¬∑) h <$> x.run :=
  rfl
#align functor.comp.run_map Functor.Comp.run_map

variable [LawfulFunctor F] [LawfulFunctor G]

variable {Œ± Œ≤ Œ≥ : Type v}

protected theorem id_map : ‚àÄ x : Comp F G Œ±, Comp.map id x = x
  | Comp.mk x => by simp [Comp.map, Functor.map_id]; rfl
                    -- ‚ä¢ mk (mk x) = mk x
                                                     -- üéâ no goals
  -- porting note: `rfl` wasn't needed in mathlib3
#align functor.comp.id_map Functor.Comp.id_map

protected theorem comp_map (g' : Œ± ‚Üí Œ≤) (h : Œ≤ ‚Üí Œ≥) :
    ‚àÄ x : Comp F G Œ±, Comp.map (h ‚àò g') x = Comp.map h (Comp.map g' x)
  | Comp.mk x => by simp [Comp.map, Comp.mk, Functor.map_comp_map, functor_norm]
                    -- üéâ no goals
  -- porting note: `Comp.mk` wasn't needed in mathlib3
#align functor.comp.comp_map Functor.Comp.comp_map

instance lawfulFunctor : LawfulFunctor (Comp F G) where
  map_const := rfl
  id_map := @Comp.id_map F G _ _ _ _
  comp_map := @Comp.comp_map F G _ _ _ _

-- porting note: had to use switch to `Id` from `id` because this has the `Functor` instance.
theorem functor_comp_id {F} [AF : Functor F] [LawfulFunctor F] :
    @Comp.functor F Id _ _ = AF :=
  @Functor.ext F _ AF (@Comp.lawfulFunctor F Id _ _ _ _) _ fun _ _ _ _ => rfl
#align functor.comp.functor_comp_id Functor.Comp.functor_comp_id

-- porting note: had to use switch to `Id` from `id` because this has the `Functor` instance.
theorem functor_id_comp {F} [AF : Functor F] [LawfulFunctor F] : @Comp.functor Id F _ _ = AF :=
  @Functor.ext F _ AF (@Comp.lawfulFunctor Id F _ _ _ _) _ fun _ _ _ _ => rfl
#align functor.comp.functor_id_comp Functor.Comp.functor_id_comp

end Comp

namespace Comp

open Function hiding comp

open Functor

variable {F : Type u ‚Üí Type w} {G : Type v ‚Üí Type u}

variable [Applicative F] [Applicative G]

/-- The `<*>` operation for the composition of applicative functors. -/
protected def seq {Œ± Œ≤ : Type v} : Comp F G (Œ± ‚Üí Œ≤) ‚Üí (Unit ‚Üí Comp F G Œ±) ‚Üí Comp F G Œ≤
  | Comp.mk f, g => match g () with
    | Comp.mk x => Comp.mk <| (¬∑ <*> ¬∑) <$> f <*> x
#align functor.comp.seq Functor.Comp.seq‚Çì
-- `‚Çì` because the type of `Seq.seq` doesn't match `has_seq.seq`

instance : Pure (Comp F G) :=
  ‚ü®fun x => Comp.mk <| pure <| pure x‚ü©

instance : Seq (Comp F G) :=
  ‚ü®fun f x => Comp.seq f x‚ü©

@[simp]
protected theorem run_pure {Œ± : Type v} : ‚àÄ x : Œ±, (pure x : Comp F G Œ±).run = pure (pure x)
  | _ => rfl
#align functor.comp.run_pure Functor.Comp.run_pure

@[simp]
protected theorem run_seq {Œ± Œ≤ : Type v} (f : Comp F G (Œ± ‚Üí Œ≤)) (x : Comp F G Œ±) :
    (f <*> x).run = (¬∑ <*> ¬∑) <$> f.run <*> x.run :=
  rfl
#align functor.comp.run_seq Functor.Comp.run_seq

instance instApplicativeComp : Applicative (Comp F G) :=
  { map := @Comp.map F G _ _, seq := @Comp.seq F G _ _ }

end Comp

variable {F : Type u ‚Üí Type u} [Functor F]

/-- If we consider `x : F Œ±` to, in some sense, contain values of type `Œ±`,
predicate `Liftp p x` holds iff every value contained by `x` satisfies `p`. -/
def Liftp {Œ± : Type u} (p : Œ± ‚Üí Prop) (x : F Œ±) : Prop :=
  ‚àÉ u : F (Subtype p), Subtype.val <$> u = x
#align functor.liftp Functor.Liftp

/-- If we consider `x : F Œ±` to, in some sense, contain values of type `Œ±`, then
`Liftr r x y` relates `x` and `y` iff (1) `x` and `y` have the same shape and
(2) we can pair values `a` from `x` and `b` from `y` so that `r a b` holds. -/
def Liftr {Œ± : Type u} (r : Œ± ‚Üí Œ± ‚Üí Prop) (x y : F Œ±) : Prop :=
  ‚àÉ u : F { p : Œ± √ó Œ± // r p.fst p.snd },
    (fun t : { p : Œ± √ó Œ± // r p.fst p.snd } => t.val.fst) <$> u = x ‚àß
      (fun t : { p : Œ± √ó Œ± // r p.fst p.snd } => t.val.snd) <$> u = y
#align functor.liftr Functor.Liftr

/-- If we consider `x : F Œ±` to, in some sense, contain values of type `Œ±`, then
`supp x` is the set of values of type `Œ±` that `x` contains. -/
def supp {Œ± : Type u} (x : F Œ±) : Set Œ± :=
  { y : Œ± | ‚àÄ ‚¶Ép‚¶Ñ, Liftp p x ‚Üí p y }
#align functor.supp Functor.supp

theorem of_mem_supp {Œ± : Type u} {x : F Œ±} {p : Œ± ‚Üí Prop} (h : Liftp p x) : ‚àÄ y ‚àà supp x, p y :=
  fun _ hy => hy h
#align functor.of_mem_supp Functor.of_mem_supp

/-- If `f` is a functor, if `fb : f Œ≤` and `a : Œ±`, then `mapConstRev fb a` is the result of
  applying `f.map` to the constant function `Œ≤ ‚Üí Œ±` sending everything to `a`, and then
  evaluating at `fb`. In other words it's `const a <$> fb`. -/
@[reducible] def mapConstRev {f : Type u ‚Üí Type v} [Functor f] {Œ± Œ≤ : Type u} :
    f Œ≤ ‚Üí Œ± ‚Üí f Œ± :=
  fun a b => Functor.mapConst b a
#align functor.map_const_rev Functor.mapConstRev
/-- If `f` is a functor, if `fb : f Œ≤` and `a : Œ±`, then `mapConstRev fb a` is the result of
  applying `f.map` to the constant function `Œ≤ ‚Üí Œ±` sending everything to `a`, and then
  evaluating at `fb`. In other words it's `const a <$> fb`. -/
infix:100 " $> " => Functor.mapConstRev

end Functor
