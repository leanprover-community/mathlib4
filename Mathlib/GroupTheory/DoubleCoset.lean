/-
Copyright (c) 2021 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/
import Mathlib.Data.Setoid.Basic
import Mathlib.GroupTheory.Subgroup.Basic
import Mathlib.GroupTheory.Coset
import Mathlib.GroupTheory.Subgroup.Pointwise
import Mathlib.Data.Set.Basic

#align_import group_theory.double_coset from "leanprover-community/mathlib"@"4c19a16e4b705bf135cf9a80ac18fcc99c438514"

/-!
# Double cosets

This file defines double cosets for two subgroups `H K` of a group `G` and the quotient of `G` by
the double coset relation, i.e. `H \ G / K`. We also prove that `G` can be written as a disjoint
union of the double cosets and that if one of `H` or `K` is the trivial group (i.e. `âŠ¥` ) then
this is the usual left or right quotient of a group by a subgroup.

## Main definitions

* `rel`: The double coset relation defined by two subgroups `H K` of `G`.
* `Doset.quotient`: The quotient of `G` by the double coset relation, i.e, `H \ G / K`.
-/
-- porting note: removed import
-- import Mathlib.Tactic.Group

variable {G : Type*} [Group G] {Î± : Type*} [Mul Î±] (J : Subgroup G) (g : G)

namespace Doset

open Pointwise

/-- The double coset as an element of `Set Î±` corresponding to `s a t` -/
def doset (a : Î±) (s t : Set Î±) : Set Î± :=
  s * {a} * t
#align doset Doset.doset

theorem mem_doset {s t : Set Î±} {a b : Î±} : b âˆˆ doset a s t â†” âˆƒ x âˆˆ s, âˆƒ y âˆˆ t, b = x * a * y :=
  âŸ¨fun âŸ¨_, y, âŸ¨x, _, hx, rfl, rflâŸ©, hy, hâŸ© => âŸ¨x, hx, y, hy, h.symmâŸ©, fun âŸ¨x, hx, y, hy, hâŸ© =>
    âŸ¨x * a, y, âŸ¨x, a, hx, rfl, rflâŸ©, hy, h.symmâŸ©âŸ©
#align doset.mem_doset Doset.mem_doset

theorem mem_doset_self (H K : Subgroup G) (a : G) : a âˆˆ doset a H K :=
  mem_doset.mpr âŸ¨1, H.one_mem, 1, K.one_mem, (one_mul a).symm.trans (mul_one (1 * a)).symmâŸ©
#align doset.mem_doset_self Doset.mem_doset_self

theorem doset_eq_of_mem {H K : Subgroup G} {a b : G} (hb : b âˆˆ doset a H K) :
    doset b H K = doset a H K := by
  obtain âŸ¨_, k, âŸ¨h, a, hh, rfl : _ = _, rflâŸ©, hk, rflâŸ© := hb
  -- âŠ¢ doset ((fun x x_1 => x * x_1) ((fun x x_1 => x * x_1) h a) k) â†‘H â†‘K = doset  â€¦
  rw [doset, doset, â† Set.singleton_mul_singleton, â† Set.singleton_mul_singleton, mul_assoc,
    mul_assoc, Subgroup.singleton_mul_subgroup hk, â† mul_assoc, â† mul_assoc,
    Subgroup.subgroup_mul_singleton hh]
#align doset.doset_eq_of_mem Doset.doset_eq_of_mem

theorem mem_doset_of_not_disjoint {H K : Subgroup G} {a b : G}
    (h : Â¬Disjoint (doset a H K) (doset b H K)) : b âˆˆ doset a H K := by
  rw [Set.not_disjoint_iff] at h
  -- âŠ¢ b âˆˆ doset a â†‘H â†‘K
  simp only [mem_doset] at *
  -- âŠ¢ âˆƒ x, x âˆˆ â†‘H âˆ§ âˆƒ y, y âˆˆ â†‘K âˆ§ b = x * a * y
  obtain âŸ¨x, âŸ¨l, hl, r, hr, hrxâŸ©, y, hy, âŸ¨r', hr', rflâŸ©âŸ© := h
  -- âŠ¢ âˆƒ x, x âˆˆ â†‘H âˆ§ âˆƒ y, y âˆˆ â†‘K âˆ§ b = x * a * y
  refine' âŸ¨yâ»Â¹ * l, H.mul_mem (H.inv_mem hy) hl, r * r'â»Â¹, K.mul_mem hr (K.inv_mem hr'), _âŸ©
  -- âŠ¢ b = yâ»Â¹ * l * a * (r * r'â»Â¹)
  rwa [mul_assoc, mul_assoc, eq_inv_mul_iff_mul_eq, â† mul_assoc, â† mul_assoc, eq_mul_inv_iff_mul_eq]
  -- ğŸ‰ no goals
#align doset.mem_doset_of_not_disjoint Doset.mem_doset_of_not_disjoint

theorem eq_of_not_disjoint {H K : Subgroup G} {a b : G}
    (h : Â¬Disjoint (doset a H K) (doset b H K)) : doset a H K = doset b H K := by
  rw [disjoint_comm] at h
  -- âŠ¢ doset a â†‘H â†‘K = doset b â†‘H â†‘K
  have ha : a âˆˆ doset b H K := mem_doset_of_not_disjoint h
  -- âŠ¢ doset a â†‘H â†‘K = doset b â†‘H â†‘K
  apply doset_eq_of_mem ha
  -- ğŸ‰ no goals
#align doset.eq_of_not_disjoint Doset.eq_of_not_disjoint

/-- The setoid defined by the double_coset relation -/
def setoid (H K : Set G) : Setoid G :=
  Setoid.ker fun x => doset x H K
#align doset.setoid Doset.setoid

/-- Quotient of `G` by the double coset relation, i.e. `H \ G / K` -/
def Quotient (H K : Set G) : Type _ :=
  _root_.Quotient (setoid H K)
#align doset.quotient Doset.Quotient

theorem rel_iff {H K : Subgroup G} {x y : G} :
    (setoid â†‘H â†‘K).Rel x y â†” âˆƒ a âˆˆ H, âˆƒ b âˆˆ K, y = a * x * b :=
  Iff.trans
    âŸ¨fun hxy => (congr_arg _ hxy).mpr (mem_doset_self H K y), fun hxy => (doset_eq_of_mem hxy).symmâŸ©
    mem_doset
#align doset.rel_iff Doset.rel_iff

theorem bot_rel_eq_leftRel (H : Subgroup G) :
    (setoid â†‘(âŠ¥ : Subgroup G) â†‘H).Rel = (QuotientGroup.leftRel H).Rel := by
  ext a b
  -- âŠ¢ Setoid.Rel (setoid â†‘âŠ¥ â†‘H) a b â†” Setoid.Rel (QuotientGroup.leftRel H) a b
  rw [rel_iff, Setoid.Rel, QuotientGroup.leftRel_apply]
  -- âŠ¢ (âˆƒ a_1, a_1 âˆˆ âŠ¥ âˆ§ âˆƒ b_1, b_1 âˆˆ H âˆ§ b = a_1 * a * b_1) â†” aâ»Â¹ * b âˆˆ H
  constructor
  -- âŠ¢ (âˆƒ a_1, a_1 âˆˆ âŠ¥ âˆ§ âˆƒ b_1, b_1 âˆˆ H âˆ§ b = a_1 * a * b_1) â†’ aâ»Â¹ * b âˆˆ H
  Â· rintro âŸ¨a, rfl : a = 1, b, hb, rflâŸ©
    -- âŠ¢ aâ»Â¹ * (1 * a * b) âˆˆ H
    change aâ»Â¹ * (1 * a * b) âˆˆ H
    -- âŠ¢ aâ»Â¹ * (1 * a * b) âˆˆ H
    rwa [one_mul, inv_mul_cancel_left]
    -- ğŸ‰ no goals
  Â· rintro (h : aâ»Â¹ * b âˆˆ H)
    -- âŠ¢ âˆƒ a_1, a_1 âˆˆ âŠ¥ âˆ§ âˆƒ b_1, b_1 âˆˆ H âˆ§ b = a_1 * a * b_1
    exact âŸ¨1, rfl, aâ»Â¹ * b, h, by rw [one_mul, mul_inv_cancel_left]âŸ©
    -- ğŸ‰ no goals
#align doset.bot_rel_eq_left_rel Doset.bot_rel_eq_leftRel

theorem rel_bot_eq_right_group_rel (H : Subgroup G) :
    (setoid â†‘H â†‘(âŠ¥ : Subgroup G)).Rel = (QuotientGroup.rightRel H).Rel := by
  ext a b
  -- âŠ¢ Setoid.Rel (setoid â†‘H â†‘âŠ¥) a b â†” Setoid.Rel (QuotientGroup.rightRel H) a b
  rw [rel_iff, Setoid.Rel, QuotientGroup.rightRel_apply]
  -- âŠ¢ (âˆƒ a_1, a_1 âˆˆ H âˆ§ âˆƒ b_1, b_1 âˆˆ âŠ¥ âˆ§ b = a_1 * a * b_1) â†” b * aâ»Â¹ âˆˆ H
  constructor
  -- âŠ¢ (âˆƒ a_1, a_1 âˆˆ H âˆ§ âˆƒ b_1, b_1 âˆˆ âŠ¥ âˆ§ b = a_1 * a * b_1) â†’ b * aâ»Â¹ âˆˆ H
  Â· rintro âŸ¨b, hb, a, rfl : a = 1, rflâŸ©
    -- âŠ¢ b * a * 1 * aâ»Â¹ âˆˆ H
    change b * a * 1 * aâ»Â¹ âˆˆ H
    -- âŠ¢ b * a * 1 * aâ»Â¹ âˆˆ H
    rwa [mul_one, mul_inv_cancel_right]
    -- ğŸ‰ no goals
  Â· rintro (h : b * aâ»Â¹ âˆˆ H)
    -- âŠ¢ âˆƒ a_1, a_1 âˆˆ H âˆ§ âˆƒ b_1, b_1 âˆˆ âŠ¥ âˆ§ b = a_1 * a * b_1
    exact âŸ¨b * aâ»Â¹, h, 1, rfl, by rw [mul_one, inv_mul_cancel_right]âŸ©
    -- ğŸ‰ no goals
#align doset.rel_bot_eq_right_group_rel Doset.rel_bot_eq_right_group_rel

/-- Create a doset out of an element of `H \ G / K`-/
def quotToDoset (H K : Subgroup G) (q : Quotient (H : Set G) K) : Set G :=
  doset q.out' H K
#align doset.quot_to_doset Doset.quotToDoset

/-- Map from `G` to `H \ G / K`-/
abbrev mk (H K : Subgroup G) (a : G) : Quotient (H : Set G) K :=
  Quotient.mk'' a
#align doset.mk Doset.mk

instance (H K : Subgroup G) : Inhabited (Quotient (H : Set G) K) :=
  âŸ¨mk H K (1 : G)âŸ©

theorem eq (H K : Subgroup G) (a b : G) :
    mk H K a = mk H K b â†” âˆƒ h âˆˆ H, âˆƒ k âˆˆ K, b = h * a * k := by
  rw [Quotient.eq'']
  -- âŠ¢ Setoid.r a b â†” âˆƒ h, h âˆˆ H âˆ§ âˆƒ k, k âˆˆ K âˆ§ b = h * a * k
  apply rel_iff
  -- ğŸ‰ no goals
#align doset.eq Doset.eq

theorem out_eq' (H K : Subgroup G) (q : Quotient â†‘H â†‘K) : mk H K q.out' = q :=
  Quotient.out_eq' q
#align doset.out_eq' Doset.out_eq'

theorem mk_out'_eq_mul (H K : Subgroup G) (g : G) :
    âˆƒ h k : G, h âˆˆ H âˆ§ k âˆˆ K âˆ§ (mk H K g : Quotient â†‘H â†‘K).out' = h * g * k := by
  have := eq H K (mk H K g : Quotient â†‘H â†‘K).out' g
  -- âŠ¢ âˆƒ h k, h âˆˆ H âˆ§ k âˆˆ K âˆ§ Quotient.out' (mk H K g) = h * g * k
  rw [out_eq'] at this
  -- âŠ¢ âˆƒ h k, h âˆˆ H âˆ§ k âˆˆ K âˆ§ Quotient.out' (mk H K g) = h * g * k
  obtain âŸ¨h, h_h, k, hk, TâŸ© := this.1 rfl
  -- âŠ¢ âˆƒ h k, h âˆˆ H âˆ§ k âˆˆ K âˆ§ Quotient.out' (mk H K g) = h * g * k
  refine' âŸ¨hâ»Â¹, kâ»Â¹, H.inv_mem h_h, K.inv_mem hk, eq_mul_inv_of_mul_eq (eq_inv_mul_of_mul_eq _)âŸ©
  -- âŠ¢ h * (Quotient.out' (mk H K g) * k) = g
  rw [â† mul_assoc, â† T]
  -- ğŸ‰ no goals
#align doset.mk_out'_eq_mul Doset.mk_out'_eq_mul

theorem mk_eq_of_doset_eq {H K : Subgroup G} {a b : G} (h : doset a H K = doset b H K) :
    mk H K a = mk H K b := by
  rw [eq]
  -- âŠ¢ âˆƒ h, h âˆˆ H âˆ§ âˆƒ k, k âˆˆ K âˆ§ b = h * a * k
  exact mem_doset.mp (h.symm â–¸ mem_doset_self H K b)
  -- ğŸ‰ no goals
#align doset.mk_eq_of_doset_eq Doset.mk_eq_of_doset_eq

theorem disjoint_out' {H K : Subgroup G} {a b : Quotient H.1 K} :
    a â‰  b â†’ Disjoint (doset a.out' H K) (doset b.out' (H : Set G) K) := by
  contrapose!
  -- âŠ¢ Â¬Disjoint (doset (Quotient.out' a) â†‘H â†‘K) (doset (Quotient.out' b) â†‘H â†‘K) â†’  â€¦
  intro h
  -- âŠ¢ a = b
  simpa [out_eq'] using mk_eq_of_doset_eq (eq_of_not_disjoint h)
  -- ğŸ‰ no goals
#align doset.disjoint_out' Doset.disjoint_out'

theorem union_quotToDoset (H K : Subgroup G) : â‹ƒ q, quotToDoset H K q = Set.univ := by
  ext x
  -- âŠ¢ x âˆˆ â‹ƒ (q : Quotient â†‘H â†‘K), quotToDoset H K q â†” x âˆˆ Set.univ
  simp only [Set.mem_iUnion, quotToDoset, mem_doset, SetLike.mem_coe, exists_prop, Set.mem_univ,
    iff_true_iff]
  use mk H K x
  -- âŠ¢ âˆƒ x_1, x_1 âˆˆ H âˆ§ âˆƒ y, y âˆˆ K âˆ§ x = x_1 * Quotient.out' (mk H K x) * y
  obtain âŸ¨h, k, h3, h4, h5âŸ© := mk_out'_eq_mul H K x
  -- âŠ¢ âˆƒ x_1, x_1 âˆˆ H âˆ§ âˆƒ y, y âˆˆ K âˆ§ x = x_1 * Quotient.out' (mk H K x) * y
  refine' âŸ¨hâ»Â¹, H.inv_mem h3, kâ»Â¹, K.inv_mem h4, _âŸ©
  -- âŠ¢ x = hâ»Â¹ * Quotient.out' (mk H K x) * kâ»Â¹
  simp only [h5, Subgroup.coe_mk, â† mul_assoc, one_mul, mul_left_inv, mul_inv_cancel_right]
  -- ğŸ‰ no goals
#align doset.union_quot_to_doset Doset.union_quotToDoset

theorem doset_union_rightCoset (H K : Subgroup G) (a : G) :
    â‹ƒ k : K, rightCoset (â†‘H) (a * k) = doset a H K := by
  ext x
  -- âŠ¢ x âˆˆ â‹ƒ (k : { x // x âˆˆ K }), rightCoset (â†‘H) (a * â†‘k) â†” x âˆˆ doset a â†‘H â†‘K
  simp only [mem_rightCoset_iff, exists_prop, mul_inv_rev, Set.mem_iUnion, mem_doset,
    Subgroup.mem_carrier, SetLike.mem_coe]
  constructor
  -- âŠ¢ (âˆƒ i, x * ((â†‘i)â»Â¹ * aâ»Â¹) âˆˆ H) â†’ âˆƒ x_1, x_1 âˆˆ H âˆ§ âˆƒ y, y âˆˆ K âˆ§ x = x_1 * a * y
  Â· rintro âŸ¨y, h_hâŸ©
    -- âŠ¢ âˆƒ x_1, x_1 âˆˆ H âˆ§ âˆƒ y, y âˆˆ K âˆ§ x = x_1 * a * y
    refine' âŸ¨x * (yâ»Â¹ * aâ»Â¹), h_h, y, y.2, _âŸ©
    -- âŠ¢ x = x * (â†‘yâ»Â¹ * aâ»Â¹) * a * â†‘y
    simp only [â† mul_assoc, Subgroup.coe_mk, inv_mul_cancel_right, SubgroupClass.coe_inv]
    -- ğŸ‰ no goals
  Â· rintro âŸ¨x, hx, y, hy, hxyâŸ©
    -- âŠ¢ âˆƒ i, xâœ * ((â†‘i)â»Â¹ * aâ»Â¹) âˆˆ H
    refine' âŸ¨âŸ¨y, hyâŸ©, _âŸ©
    -- âŠ¢ xâœ * ((â†‘{ val := y, property := hy })â»Â¹ * aâ»Â¹) âˆˆ H
    simp only [hxy, â† mul_assoc, hx, mul_inv_cancel_right, Subgroup.coe_mk]
    -- ğŸ‰ no goals
#align doset.doset_union_right_coset Doset.doset_union_rightCoset

theorem doset_union_leftCoset (H K : Subgroup G) (a : G) :
    â‹ƒ h : H, leftCoset (h * a : G) K = doset a H K := by
  ext x
  -- âŠ¢ x âˆˆ â‹ƒ (h : { x // x âˆˆ H }), leftCoset (â†‘h * a) â†‘K â†” x âˆˆ doset a â†‘H â†‘K
  simp only [mem_leftCoset_iff, mul_inv_rev, Set.mem_iUnion, mem_doset]
  -- âŠ¢ (âˆƒ i, aâ»Â¹ * (â†‘i)â»Â¹ * x âˆˆ â†‘K) â†” âˆƒ x_1, x_1 âˆˆ â†‘H âˆ§ âˆƒ y, y âˆˆ â†‘K âˆ§ x = x_1 * a * y
  constructor
  -- âŠ¢ (âˆƒ i, aâ»Â¹ * (â†‘i)â»Â¹ * x âˆˆ â†‘K) â†’ âˆƒ x_1, x_1 âˆˆ â†‘H âˆ§ âˆƒ y, y âˆˆ â†‘K âˆ§ x = x_1 * a * y
  Â· rintro âŸ¨y, h_hâŸ©
    -- âŠ¢ âˆƒ x_1, x_1 âˆˆ â†‘H âˆ§ âˆƒ y, y âˆˆ â†‘K âˆ§ x = x_1 * a * y
    refine' âŸ¨y, y.2, aâ»Â¹ * yâ»Â¹ * x, h_h, _âŸ©
    -- âŠ¢ x = â†‘y * a * (aâ»Â¹ * â†‘yâ»Â¹ * x)
    simp only [â† mul_assoc, one_mul, mul_right_inv, mul_inv_cancel_right, SubgroupClass.coe_inv]
    -- ğŸ‰ no goals
  Â· rintro âŸ¨x, hx, y, hy, hxyâŸ©
    -- âŠ¢ âˆƒ i, aâ»Â¹ * (â†‘i)â»Â¹ * xâœ âˆˆ â†‘K
    refine' âŸ¨âŸ¨x, hxâŸ©, _âŸ©
    -- âŠ¢ aâ»Â¹ * (â†‘{ val := x, property := hx })â»Â¹ * xâœ âˆˆ â†‘K
    simp only [hxy, â† mul_assoc, hy, one_mul, mul_left_inv, Subgroup.coe_mk, inv_mul_cancel_right]
    -- ğŸ‰ no goals
#align doset.doset_union_left_coset Doset.doset_union_leftCoset

theorem left_bot_eq_left_quot (H : Subgroup G) :
    Quotient (âŠ¥ : Subgroup G).1 (H : Set G) = (G â§¸ H) := by
  unfold Quotient
  -- âŠ¢ _root_.Quotient (setoid â†‘âŠ¥.toSubmonoid â†‘H) = (G â§¸ H)
  congr
  -- âŠ¢ setoid â†‘âŠ¥.toSubmonoid â†‘H = QuotientGroup.leftRel H
  ext
  -- âŠ¢ Setoid.Rel (setoid â†‘âŠ¥.toSubmonoid â†‘H) aâœ bâœ â†” Setoid.Rel (QuotientGroup.left â€¦
  simp_rw [â† bot_rel_eq_leftRel H]
  -- âŠ¢ Setoid.Rel (setoid â†‘âŠ¥.toSubmonoid â†‘H) aâœ bâœ â†” Setoid.Rel (setoid â†‘âŠ¥ â†‘H) aâœ bâœ
  rfl
  -- ğŸ‰ no goals
#align doset.left_bot_eq_left_quot Doset.left_bot_eq_left_quot

theorem right_bot_eq_right_quot (H : Subgroup G) :
    Quotient (H.1 : Set G) (âŠ¥ : Subgroup G) = _root_.Quotient (QuotientGroup.rightRel H) := by
  unfold Quotient
  -- âŠ¢ _root_.Quotient (setoid â†‘H.toSubmonoid â†‘âŠ¥) = _root_.Quotient (QuotientGroup. â€¦
  congr
  -- âŠ¢ setoid â†‘H.toSubmonoid â†‘âŠ¥ = QuotientGroup.rightRel H
  ext
  -- âŠ¢ Setoid.Rel (setoid â†‘H.toSubmonoid â†‘âŠ¥) aâœ bâœ â†” Setoid.Rel (QuotientGroup.righ â€¦
  simp_rw [â† rel_bot_eq_right_group_rel H]
  -- âŠ¢ Setoid.Rel (setoid â†‘H.toSubmonoid â†‘âŠ¥) aâœ bâœ â†” Setoid.Rel (setoid â†‘H â†‘âŠ¥) aâœ bâœ
  rfl
  -- ğŸ‰ no goals
#align doset.right_bot_eq_right_quot Doset.right_bot_eq_right_quot

end Doset
