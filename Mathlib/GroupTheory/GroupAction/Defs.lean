/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Yury Kudryashov
-/
import Mathlib.Algebra.Group.TypeTags
import Mathlib.Algebra.Group.Commute
import Mathlib.Algebra.Hom.Group
import Mathlib.Algebra.Opposites
import Mathlib.Logic.Embedding.Basic

#align_import group_theory.group_action.defs from "leanprover-community/mathlib"@"dad7ecf9a1feae63e6e49f07619b7087403fb8d4"

/-!
# Definitions of group actions

This file defines a hierarchy of group action type-classes on top of the previously defined
notation classes `SMul` and its additive version `VAdd`:

* `MulAction M Œ±` and its additive version `AddAction G P` are typeclasses used for
  actions of multiplicative and additive monoids and groups; they extend notation classes
  `SMul` and `VAdd` that are defined in `Algebra.Group.Defs`;
* `DistribMulAction M A` is a typeclass for an action of a multiplicative monoid on
  an additive monoid such that `a ‚Ä¢ (b + c) = a ‚Ä¢ b + a ‚Ä¢ c` and `a ‚Ä¢ 0 = 0`.

The hierarchy is extended further by `Module`, defined elsewhere.

Also provided are typeclasses for faithful and transitive actions, and typeclasses regarding the
interaction of different group actions,

* `SMulCommClass M N Œ±` and its additive version `VAddCommClass M N Œ±`;
* `IsScalarTower M N Œ±` and its additive version `VAddAssocClass M N Œ±`;
* `IsCentralScalar M Œ±` and its additive version `IsCentralVAdd M N Œ±`.

## Notation

- `a ‚Ä¢ b` is used as notation for `SMul.smul a b`.
- `a +·µ• b` is used as notation for `VAdd.vadd a b`.

## Implementation details

This file should avoid depending on other parts of `GroupTheory`, to avoid import cycles.
More sophisticated lemmas belong in `GroupTheory.GroupAction`.

## Tags

group action
-/


variable {M N G A B Œ± Œ≤ Œ≥ Œ¥ : Type*}

open Function (Injective Surjective)

/-!
### Faithful actions
-/


/-- Typeclass for faithful actions. -/
class FaithfulVAdd (G : Type*) (P : Type*) [VAdd G P] : Prop where
  /-- Two elements `g‚ÇÅ` and `g‚ÇÇ` are equal whenever they act in the same way on all points. -/
  eq_of_vadd_eq_vadd : ‚àÄ {g‚ÇÅ g‚ÇÇ : G}, (‚àÄ p : P, g‚ÇÅ +·µ• p = g‚ÇÇ +·µ• p) ‚Üí g‚ÇÅ = g‚ÇÇ
#align has_faithful_vadd FaithfulVAdd

/-- Typeclass for faithful actions. -/
@[to_additive]
class FaithfulSMul (M : Type*) (Œ± : Type*) [SMul M Œ±] : Prop where
  /-- Two elements `m‚ÇÅ` and `m‚ÇÇ` are equal whenever they act in the same way on all points. -/
  eq_of_smul_eq_smul : ‚àÄ {m‚ÇÅ m‚ÇÇ : M}, (‚àÄ a : Œ±, m‚ÇÅ ‚Ä¢ a = m‚ÇÇ ‚Ä¢ a) ‚Üí m‚ÇÅ = m‚ÇÇ
#align has_faithful_smul FaithfulSMul

export FaithfulSMul (eq_of_smul_eq_smul)

export FaithfulVAdd (eq_of_vadd_eq_vadd)

@[to_additive]
theorem smul_left_injective' [SMul M Œ±] [FaithfulSMul M Œ±] :
    Function.Injective ((¬∑ ‚Ä¢ ¬∑) : M ‚Üí Œ± ‚Üí Œ±) := fun _ _ h =>
  FaithfulSMul.eq_of_smul_eq_smul (congr_fun h)
#align smul_left_injective' smul_left_injective'
#align vadd_left_injective' vadd_left_injective'

-- see Note [lower instance priority]
/-- See also `Monoid.toMulAction` and `MulZeroClass.toSMulWithZero`. -/
@[to_additive "See also `AddMonoid.toAddAction`"]
instance (priority := 910) Mul.toSMul (Œ± : Type*) [Mul Œ±] : SMul Œ± Œ± :=
  ‚ü®(¬∑ * ¬∑)‚ü©
#align has_mul.to_has_smul Mul.toSMul
#align has_add.to_has_vadd Add.toVAdd

@[to_additive (attr := simp)]
theorem smul_eq_mul (Œ± : Type*) [Mul Œ±] {a a' : Œ±} : a ‚Ä¢ a' = a * a' :=
  rfl
#align smul_eq_mul smul_eq_mul
#align vadd_eq_add vadd_eq_add

/-- Type class for additive monoid actions. -/
class AddAction (G : Type*) (P : Type*) [AddMonoid G] extends VAdd G P where
  /-- Zero is a neutral element for `+·µ•` -/
  protected zero_vadd : ‚àÄ p : P, (0 : G) +·µ• p = p
  /-- Associativity of `+` and `+·µ•` -/
  add_vadd : ‚àÄ (g‚ÇÅ g‚ÇÇ : G) (p : P), g‚ÇÅ + g‚ÇÇ +·µ• p = g‚ÇÅ +·µ• (g‚ÇÇ +·µ• p)
#align add_action AddAction

/-- Typeclass for multiplicative actions by monoids. This generalizes group actions. -/
@[to_additive (attr := ext)]
class MulAction (Œ± : Type*) (Œ≤ : Type*) [Monoid Œ±] extends SMul Œ± Œ≤ where
  /-- One is the neutral element for `‚Ä¢` -/
  protected one_smul : ‚àÄ b : Œ≤, (1 : Œ±) ‚Ä¢ b = b
  /-- Associativity of `‚Ä¢` and `*` -/
  mul_smul : ‚àÄ (x y : Œ±) (b : Œ≤), (x * y) ‚Ä¢ b = x ‚Ä¢ y ‚Ä¢ b
#align mul_action MulAction
#align mul_action.ext MulAction.ext
#align add_action.ext_iff AddAction.ext_iff
#align mul_action.ext_iff MulAction.ext_iff
#align add_action.ext AddAction.ext

/-!
### (Pre)transitive action

`M` acts pretransitively on `Œ±` if for any `x y` there is `g` such that `g ‚Ä¢ x = y` (or `g +·µ• x = y`
for an additive action). A transitive action should furthermore have `Œ±` nonempty.

In this section we define typeclasses `MulAction.IsPretransitive` and
`AddAction.IsPretransitive` and provide `MulAction.exists_smul_eq`/`AddAction.exists_vadd_eq`,
`MulAction.surjective_smul`/`AddAction.surjective_vadd` as public interface to access this
property. We do not provide typeclasses `*Action.IsTransitive`; users should assume
`[MulAction.IsPretransitive M Œ±] [Nonempty Œ±]` instead. -/


/-- `M` acts pretransitively on `Œ±` if for any `x y` there is `g` such that `g +·µ• x = y`.
  A transitive action should furthermore have `Œ±` nonempty. -/
class AddAction.IsPretransitive (M Œ± : Type*) [VAdd M Œ±] : Prop where
  /-- There is `g` such that `g +·µ• x = y`. -/
  exists_vadd_eq : ‚àÄ x y : Œ±, ‚àÉ g : M, g +·µ• x = y
#align add_action.is_pretransitive AddAction.IsPretransitive

/-- `M` acts pretransitively on `Œ±` if for any `x y` there is `g` such that `g ‚Ä¢ x = y`.
  A transitive action should furthermore have `Œ±` nonempty. -/
@[to_additive]
class MulAction.IsPretransitive (M Œ± : Type*) [SMul M Œ±] : Prop where
  /-- There is `g` such that `g ‚Ä¢ x = y`. -/
  exists_smul_eq : ‚àÄ x y : Œ±, ‚àÉ g : M, g ‚Ä¢ x = y
#align mul_action.is_pretransitive MulAction.IsPretransitive

namespace MulAction

variable (M) [SMul M Œ±] [IsPretransitive M Œ±]

@[to_additive]
theorem exists_smul_eq (x y : Œ±) : ‚àÉ m : M, m ‚Ä¢ x = y :=
  IsPretransitive.exists_smul_eq x y
#align mul_action.exists_smul_eq MulAction.exists_smul_eq
#align add_action.exists_vadd_eq AddAction.exists_vadd_eq

@[to_additive]
theorem surjective_smul (x : Œ±) : Surjective fun c : M => c ‚Ä¢ x :=
  exists_smul_eq M x
#align mul_action.surjective_smul MulAction.surjective_smul
#align add_action.surjective_vadd AddAction.surjective_vadd

/-- The regular action of a group on itself is transitive. -/
@[to_additive "The regular action of a group on itself is transitive."]
instance Regular.isPretransitive [Group G] : IsPretransitive G G :=
  ‚ü®fun x y => ‚ü®y * x‚Åª¬π, inv_mul_cancel_right _ _‚ü©‚ü©
#align mul_action.regular.is_pretransitive MulAction.Regular.isPretransitive
#align add_action.regular.is_pretransitive AddAction.Regular.isPretransitive

end MulAction

/-!
### Scalar tower and commuting actions
-/


/-- A typeclass mixin saying that two additive actions on the same space commute. -/
class VAddCommClass (M N Œ± : Type*) [VAdd M Œ±] [VAdd N Œ±] : Prop where
  /-- `+·µ•` is left commutative -/
  vadd_comm : ‚àÄ (m : M) (n : N) (a : Œ±), m +·µ• (n +·µ• a) = n +·µ• (m +·µ• a)
#align vadd_comm_class VAddCommClass

/-- A typeclass mixin saying that two multiplicative actions on the same space commute. -/
@[to_additive]
class SMulCommClass (M N Œ± : Type*) [SMul M Œ±] [SMul N Œ±] : Prop where
  /-- `‚Ä¢` is left commutative -/
  smul_comm : ‚àÄ (m : M) (n : N) (a : Œ±), m ‚Ä¢ n ‚Ä¢ a = n ‚Ä¢ m ‚Ä¢ a
#align smul_comm_class SMulCommClass

export MulAction (mul_smul)

export AddAction (add_vadd)

export SMulCommClass (smul_comm)

export VAddCommClass (vadd_comm)

library_note "bundled maps over different rings"/--
Frequently, we find ourselves wanting to express a bilinear map `M ‚Üí‚Çó[R] N ‚Üí‚Çó[R] P` or an
equivalence between maps `(M ‚Üí‚Çó[R] N) ‚âÉ‚Çó[R] (M' ‚Üí‚Çó[R] N')` where the maps have an associated ring
`R`. Unfortunately, using definitions like these requires that `R` satisfy `CommSemiring R`, and
not just `Semiring R`. Using `M ‚Üí‚Çó[R] N ‚Üí+ P` and `(M ‚Üí‚Çó[R] N) ‚âÉ+ (M' ‚Üí‚Çó[R] N')` avoids this
problem, but throws away structure that is useful for when we _do_ have a commutative (semi)ring.

To avoid making this compromise, we instead state these definitions as `M ‚Üí‚Çó[R] N ‚Üí‚Çó[S] P` or
`(M ‚Üí‚Çó[R] N) ‚âÉ‚Çó[S] (M' ‚Üí‚Çó[R] N')` and require `SMulCommClass S R` on the appropriate modules. When
the caller has `CommSemiring R`, they can set `S = R` and `smulCommClass_self` will populate the
instance. If the caller only has `Semiring R` they can still set either `R = ‚Ñï` or `S = ‚Ñï`, and
`AddCommMonoid.nat_smulCommClass` or `AddCommMonoid.nat_smulCommClass'` will populate
the typeclass, which is still sufficient to recover a `‚âÉ+` or `‚Üí+` structure.

An example of where this is used is `LinearMap.prod_equiv`.
-/


/-- Commutativity of actions is a symmetric relation. This lemma can't be an instance because this
would cause a loop in the instance search graph. -/
@[to_additive]
theorem SMulCommClass.symm (M N Œ± : Type*) [SMul M Œ±] [SMul N Œ±] [SMulCommClass M N Œ±] :
    SMulCommClass N M Œ± :=
  ‚ü®fun a' a b => (smul_comm a a' b).symm‚ü©
#align smul_comm_class.symm SMulCommClass.symm
#align vadd_comm_class.symm VAddCommClass.symm

/-- Commutativity of additive actions is a symmetric relation. This lemma can't be an instance
because this would cause a loop in the instance search graph. -/
add_decl_doc VAddCommClass.symm

theorem Function.Injective.smulCommClass [SMul M Œ±] [SMul N Œ±] [SMul M Œ≤] [SMul N Œ≤]
    [SMulCommClass M N Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Function.Injective f)
    (h‚ÇÅ : ‚àÄ (c : M) x, f (c ‚Ä¢ x) = c ‚Ä¢ f x) (h‚ÇÇ : ‚àÄ (c : N) x, f (c ‚Ä¢ x) = c ‚Ä¢ f x) :
    SMulCommClass M N Œ± where
  smul_comm c‚ÇÅ c‚ÇÇ x := hf <| by simp only [h‚ÇÅ, h‚ÇÇ, smul_comm c‚ÇÅ c‚ÇÇ (f x)]
                                -- üéâ no goals

theorem Function.Surjective.smulCommClass [SMul M Œ±] [SMul N Œ±] [SMul M Œ≤] [SMul N Œ≤]
    [SMulCommClass M N Œ±] {f : Œ± ‚Üí Œ≤} (hf : Function.Surjective f)
    (h‚ÇÅ : ‚àÄ (c : M) x, f (c ‚Ä¢ x) = c ‚Ä¢ f x) (h‚ÇÇ : ‚àÄ (c : N) x, f (c ‚Ä¢ x) = c ‚Ä¢ f x) :
    SMulCommClass M N Œ≤ where
  smul_comm c‚ÇÅ c‚ÇÇ := hf.forall.2 fun x ‚Ü¶ by simp only [‚Üê h‚ÇÅ, ‚Üê h‚ÇÇ, smul_comm c‚ÇÅ c‚ÇÇ x]
                                            -- üéâ no goals

@[to_additive]
instance smulCommClass_self (M Œ± : Type*) [CommMonoid M] [MulAction M Œ±] : SMulCommClass M M Œ± :=
  ‚ü®fun a a' b => by rw [‚Üê mul_smul, mul_comm, mul_smul]‚ü©
                    -- üéâ no goals
#align smul_comm_class_self smulCommClass_self
#align vadd_comm_class_self vaddCommClass_self

/-- An instance of `VAddAssocClass M N Œ±` states that the additive action of `M` on `Œ±` is
determined by the additive actions of `M` on `N` and `N` on `Œ±`. -/
class VAddAssocClass (M N Œ± : Type*) [VAdd M N] [VAdd N Œ±] [VAdd M Œ±] : Prop where
  /-- Associativity of `+·µ•` -/
  vadd_assoc : ‚àÄ (x : M) (y : N) (z : Œ±), x +·µ• y +·µ• z = x +·µ• (y +·µ• z)
#align vadd_assoc_class VAddAssocClass

/-- An instance of `IsScalarTower M N Œ±` states that the multiplicative
action of `M` on `Œ±` is determined by the multiplicative actions of `M` on `N`
and `N` on `Œ±`. -/
@[to_additive VAddAssocClass] -- TODO auto-translating
class IsScalarTower (M N Œ± : Type*) [SMul M N] [SMul N Œ±] [SMul M Œ±] : Prop where
  /-- Associativity of `‚Ä¢` -/
  smul_assoc : ‚àÄ (x : M) (y : N) (z : Œ±), (x ‚Ä¢ y) ‚Ä¢ z = x ‚Ä¢ y ‚Ä¢ z
#align is_scalar_tower IsScalarTower

@[to_additive (attr := simp)]
theorem smul_assoc {M N} [SMul M N] [SMul N Œ±] [SMul M Œ±] [IsScalarTower M N Œ±] (x : M)
    (y : N) (z : Œ±) : (x ‚Ä¢ y) ‚Ä¢ z = x ‚Ä¢ y ‚Ä¢ z :=
  IsScalarTower.smul_assoc x y z
#align smul_assoc smul_assoc
#align vadd_assoc vadd_assoc

@[to_additive]
instance Semigroup.isScalarTower [Semigroup Œ±] : IsScalarTower Œ± Œ± Œ± :=
  ‚ü®mul_assoc‚ü©
#align semigroup.is_scalar_tower Semigroup.isScalarTower
#align add_semigroup.vadd_assoc_class AddSemigroup.isScalarTower

/-- A typeclass indicating that the right (aka `AddOpposite`) and left actions by `M` on `Œ±` are
equal, that is that `M` acts centrally on `Œ±`. This can be thought of as a version of commutativity
for `+·µ•`. -/
class IsCentralVAdd (M Œ± : Type*) [VAdd M Œ±] [VAdd M·µÉ·µí·µñ Œ±] : Prop where
  /-- The right and left actions of `M` on `Œ±` are equal. -/
  op_vadd_eq_vadd : ‚àÄ (m : M) (a : Œ±), AddOpposite.op m +·µ• a = m +·µ• a
#align is_central_vadd IsCentralVAdd

/-- A typeclass indicating that the right (aka `MulOpposite`) and left actions by `M` on `Œ±` are
equal, that is that `M` acts centrally on `Œ±`. This can be thought of as a version of commutativity
for `‚Ä¢`. -/
@[to_additive]
class IsCentralScalar (M Œ± : Type*) [SMul M Œ±] [SMul M·µê·µí·µñ Œ±] : Prop where
  /-- The right and left actions of `M` on `Œ±` are equal. -/
  op_smul_eq_smul : ‚àÄ (m : M) (a : Œ±), MulOpposite.op m ‚Ä¢ a = m ‚Ä¢ a
#align is_central_scalar IsCentralScalar

attribute [simp] IsCentralScalar.op_smul_eq_smul

@[to_additive]
theorem IsCentralScalar.unop_smul_eq_smul {M Œ± : Type*} [SMul M Œ±] [SMul M·µê·µí·µñ Œ±]
    [IsCentralScalar M Œ±] (m : M·µê·µí·µñ) (a : Œ±) : MulOpposite.unop m ‚Ä¢ a = m ‚Ä¢ a := by
  induction m using MulOpposite.rec'
  -- ‚ä¢ MulOpposite.unop (MulOpposite.op X‚úù) ‚Ä¢ a = MulOpposite.op X‚úù ‚Ä¢ a
  exact (IsCentralScalar.op_smul_eq_smul _ a).symm
  -- üéâ no goals
#align is_central_scalar.unop_smul_eq_smul IsCentralScalar.unop_smul_eq_smul
#align is_central_vadd.unop_vadd_eq_vadd IsCentralVAdd.unop_vadd_eq_vadd

export IsCentralVAdd (op_vadd_eq_vadd unop_vadd_eq_vadd)

export IsCentralScalar (op_smul_eq_smul unop_smul_eq_smul)

-- these instances are very low priority, as there is usually a faster way to find these instances
@[to_additive]
instance (priority := 50) SMulCommClass.op_left [SMul M Œ±] [SMul M·µê·µí·µñ Œ±] [IsCentralScalar M Œ±]
    [SMul N Œ±] [SMulCommClass M N Œ±] : SMulCommClass M·µê·µí·µñ N Œ± :=
  ‚ü®fun m n a => by rw [‚Üê unop_smul_eq_smul m (n ‚Ä¢ a), ‚Üê unop_smul_eq_smul m a, smul_comm]‚ü©
                   -- üéâ no goals
#align smul_comm_class.op_left SMulCommClass.op_left
#align vadd_comm_class.op_left VAddCommClass.op_left

@[to_additive]
instance (priority := 50) SMulCommClass.op_right [SMul M Œ±] [SMul N Œ±] [SMul N·µê·µí·µñ Œ±]
    [IsCentralScalar N Œ±] [SMulCommClass M N Œ±] : SMulCommClass M N·µê·µí·µñ Œ± :=
  ‚ü®fun m n a => by rw [‚Üê unop_smul_eq_smul n (m ‚Ä¢ a), ‚Üê unop_smul_eq_smul n a, smul_comm]‚ü©
                   -- üéâ no goals
#align smul_comm_class.op_right SMulCommClass.op_right
#align vadd_comm_class.op_right VAddCommClass.op_right

@[to_additive]
instance (priority := 50) IsScalarTower.op_left [SMul M Œ±] [SMul M·µê·µí·µñ Œ±] [IsCentralScalar M Œ±]
    [SMul M N] [SMul M·µê·µí·µñ N] [IsCentralScalar M N] [SMul N Œ±] [IsScalarTower M N Œ±] :
    IsScalarTower M·µê·µí·µñ N Œ± :=
  ‚ü®fun m n a => by rw [‚Üê unop_smul_eq_smul m (n ‚Ä¢ a), ‚Üê unop_smul_eq_smul m n, smul_assoc]‚ü©
                   -- üéâ no goals
#align is_scalar_tower.op_left IsScalarTower.op_left
#align vadd_assoc_class.op_left VAddAssocClass.op_left

@[to_additive]
instance (priority := 50) IsScalarTower.op_right [SMul M Œ±] [SMul M N] [SMul N Œ±]
    [SMul N·µê·µí·µñ Œ±] [IsCentralScalar N Œ±] [IsScalarTower M N Œ±] : IsScalarTower M N·µê·µí·µñ Œ± :=
  ‚ü®fun m n a => by
    rw [‚Üê unop_smul_eq_smul n a, ‚Üê unop_smul_eq_smul (m ‚Ä¢ n) a, MulOpposite.unop_smul, smul_assoc]‚ü©
    -- üéâ no goals
#align is_scalar_tower.op_right IsScalarTower.op_right
#align vadd_assoc_class.op_right VAddAssocClass.op_right

namespace SMul

variable [SMul M Œ±]

/-- Auxiliary definition for `SMul.comp`, `MulAction.compHom`,
`DistribMulAction.compHom`, `Module.compHom`, etc. -/
@[to_additive (attr := simp) " Auxiliary definition for `VAdd.comp`, `AddAction.compHom`, etc. "]
def comp.smul (g : N ‚Üí M) (n : N) (a : Œ±) : Œ± :=
  g n ‚Ä¢ a
#align has_smul.comp.smul SMul.comp.smul
#align has_vadd.comp.vadd VAdd.comp.vadd

variable (Œ±)

/-- An action of `M` on `Œ±` and a function `N ‚Üí M` induces an action of `N` on `Œ±`.

See note [reducible non-instances]. Since this is reducible, we make sure to go via
`SMul.comp.smul` to prevent typeclass inference unfolding too far. -/
@[to_additive (attr := reducible)
      "An additive action of `M` on `Œ±` and a function `N ‚Üí M` induces
       an additive action of `N` on `Œ±` "]
def comp (g : N ‚Üí M) : SMul N Œ± where smul := SMul.comp.smul g
#align has_smul.comp SMul.comp
#align has_vadd.comp VAdd.comp

variable {Œ±}

/-- Given a tower of scalar actions `M ‚Üí Œ± ‚Üí Œ≤`, if we use `SMul.comp`
to pull back both of `M`'s actions by a map `g : N ‚Üí M`, then we obtain a new
tower of scalar actions `N ‚Üí Œ± ‚Üí Œ≤`.

This cannot be an instance because it can cause infinite loops whenever the `SMul` arguments
are still metavariables.
-/
@[to_additive
      "Given a tower of additive actions `M ‚Üí Œ± ‚Üí Œ≤`, if we use `SMul.comp` to pull back both of
       `M`'s actions by a map `g : N ‚Üí M`, then we obtain a new tower of scalar actions `N ‚Üí Œ± ‚Üí Œ≤`.

       This cannot be an instance because it can cause infinite loops whenever the `SMul` arguments
       are still metavariables."]
theorem comp.isScalarTower [SMul M Œ≤] [SMul Œ± Œ≤] [IsScalarTower M Œ± Œ≤] (g : N ‚Üí M) : by
    haveI := comp Œ± g; haveI := comp Œ≤ g; exact IsScalarTower N Œ± Œ≤ :=
    -- ‚ä¢ Sort ?u.10323
                       -- ‚ä¢ Sort ?u.10323
                                          -- üéâ no goals
  { comp Œ± g, comp Œ≤ g with
    smul_assoc := fun n => smul_assoc (g n) }
#align has_smul.comp.is_scalar_tower SMul.comp.isScalarTower
#align has_vadd.comp.vadd_assoc_class VAdd.comp.isScalarTower

/-- This cannot be an instance because it can cause infinite loops whenever the `SMul` arguments
are still metavariables.
-/
@[to_additive
      "This cannot be an instance because it can cause infinite loops whenever the `VAdd` arguments
       are still metavariables."]
theorem comp.smulCommClass [SMul Œ≤ Œ±] [SMulCommClass M Œ≤ Œ±] (g : N ‚Üí M) :
    haveI := comp Œ± g
    SMulCommClass N Œ≤ Œ± :=
  { comp Œ± g with
    smul_comm := fun n => smul_comm (g n) }
#align has_smul.comp.smul_comm_class SMul.comp.smulCommClass
#align has_vadd.comp.vadd_comm_class VAdd.comp.vaddCommClass

/-- This cannot be an instance because it can cause infinite loops whenever the `SMul` arguments
are still metavariables.
-/
@[to_additive
      "This cannot be an instance because it can cause infinite loops whenever the `VAdd` arguments
       are still metavariables."]
theorem comp.smulCommClass' [SMul Œ≤ Œ±] [SMulCommClass Œ≤ M Œ±] (g : N ‚Üí M) :
    haveI := comp Œ± g
    SMulCommClass Œ≤ N Œ± :=
  { comp Œ± g with
    smul_comm := fun _ n => smul_comm _ (g n) }
#align has_smul.comp.smul_comm_class' SMul.comp.smulCommClass'
#align has_vadd.comp.vadd_comm_class' VAdd.comp.vaddCommClass'

end SMul

section

/-- Note that the `SMulCommClass Œ± Œ≤ Œ≤` typeclass argument is usually satisfied by `Algebra Œ± Œ≤`.
-/
@[to_additive] -- Porting note: nolint to_additive_doc
theorem mul_smul_comm [Mul Œ≤] [SMul Œ± Œ≤] [SMulCommClass Œ± Œ≤ Œ≤] (s : Œ±) (x y : Œ≤) :
    x * s ‚Ä¢ y = s ‚Ä¢ (x * y) :=
  (smul_comm s x y).symm
#align mul_smul_comm mul_smul_comm
#align add_vadd_comm add_vadd_comm

/-- Note that the `IsScalarTower Œ± Œ≤ Œ≤` typeclass argument is usually satisfied by `Algebra Œ± Œ≤`.
-/
@[to_additive] -- Porting note: nolint to_additive_doc
theorem smul_mul_assoc [Mul Œ≤] [SMul Œ± Œ≤] [IsScalarTower Œ± Œ≤ Œ≤] (r : Œ±) (x y : Œ≤) :
    r ‚Ä¢ x * y = r ‚Ä¢ (x * y) :=
  smul_assoc r x y
#align smul_mul_assoc smul_mul_assoc
#align vadd_add_assoc vadd_add_assoc

@[to_additive]
theorem smul_smul_smul_comm [SMul Œ± Œ≤] [SMul Œ± Œ≥] [SMul Œ≤ Œ¥] [SMul Œ± Œ¥] [SMul Œ≥ Œ¥]
    [IsScalarTower Œ± Œ≤ Œ¥] [IsScalarTower Œ± Œ≥ Œ¥] [SMulCommClass Œ≤ Œ≥ Œ¥] (a : Œ±) (b : Œ≤) (c : Œ≥)
    (d : Œ¥) : (a ‚Ä¢ b) ‚Ä¢ c ‚Ä¢ d = (a ‚Ä¢ c) ‚Ä¢ b ‚Ä¢ d := by
  rw [smul_assoc, smul_assoc, smul_comm b]
  -- üéâ no goals
#align smul_smul_smul_comm smul_smul_smul_comm
#align vadd_vadd_vadd_comm vadd_vadd_vadd_comm

variable [SMul M Œ±]

@[to_additive]
theorem Commute.smul_right [Mul Œ±] [SMulCommClass M Œ± Œ±] [IsScalarTower M Œ± Œ±] {a b : Œ±}
    (h : Commute a b) (r : M) : Commute a (r ‚Ä¢ b) :=
  (mul_smul_comm _ _ _).trans ((congr_arg _ h).trans <| (smul_mul_assoc _ _ _).symm)
#align commute.smul_right Commute.smul_right
#align add_commute.vadd_right AddCommute.vadd_right

@[to_additive]
theorem Commute.smul_left [Mul Œ±] [SMulCommClass M Œ± Œ±] [IsScalarTower M Œ± Œ±] {a b : Œ±}
    (h : Commute a b) (r : M) : Commute (r ‚Ä¢ a) b :=
  (h.symm.smul_right r).symm
#align commute.smul_left Commute.smul_left
#align add_commute.vadd_left AddCommute.vadd_left

end

section ite

variable [SMul M Œ±] (p : Prop) [Decidable p]

@[to_additive]
theorem ite_smul (a‚ÇÅ a‚ÇÇ : M) (b : Œ±) : ite p a‚ÇÅ a‚ÇÇ ‚Ä¢ b = ite p (a‚ÇÅ ‚Ä¢ b) (a‚ÇÇ ‚Ä¢ b) := by
  split_ifs <;> rfl
  -- ‚ä¢ a‚ÇÅ ‚Ä¢ b = a‚ÇÅ ‚Ä¢ b
                -- üéâ no goals
                -- üéâ no goals
#align ite_smul ite_smul
#align ite_vadd ite_vadd

@[to_additive]
theorem smul_ite (a : M) (b‚ÇÅ b‚ÇÇ : Œ±) : a ‚Ä¢ ite p b‚ÇÅ b‚ÇÇ = ite p (a ‚Ä¢ b‚ÇÅ) (a ‚Ä¢ b‚ÇÇ) := by
  split_ifs <;> rfl
  -- ‚ä¢ a ‚Ä¢ b‚ÇÅ = a ‚Ä¢ b‚ÇÅ
                -- üéâ no goals
                -- üéâ no goals
#align smul_ite smul_ite
#align vadd_ite vadd_ite

end ite

section

variable [Monoid M] [MulAction M Œ±]

@[to_additive]
theorem smul_smul (a‚ÇÅ a‚ÇÇ : M) (b : Œ±) : a‚ÇÅ ‚Ä¢ a‚ÇÇ ‚Ä¢ b = (a‚ÇÅ * a‚ÇÇ) ‚Ä¢ b :=
  (mul_smul _ _ _).symm
#align smul_smul smul_smul
#align vadd_vadd vadd_vadd

variable (M)

@[to_additive (attr := simp)]
theorem one_smul (b : Œ±) : (1 : M) ‚Ä¢ b = b :=
  MulAction.one_smul _
#align one_smul one_smul
#align zero_vadd zero_vadd

/-- `SMul` version of `one_mul_eq_id` -/
@[to_additive "`VAdd` version of `zero_add_eq_id`"]
theorem one_smul_eq_id : ((¬∑ ‚Ä¢ ¬∑) (1 : M) : Œ± ‚Üí Œ±) = id :=
  funext <| one_smul _
#align one_smul_eq_id one_smul_eq_id
#align zero_vadd_eq_id zero_vadd_eq_id

/-- `SMul` version of `comp_mul_left` -/
@[to_additive "`VAdd` version of `comp_add_left`"]
theorem comp_smul_left (a‚ÇÅ a‚ÇÇ : M) : (¬∑ ‚Ä¢ ¬∑) a‚ÇÅ ‚àò (¬∑ ‚Ä¢ ¬∑) a‚ÇÇ = ((¬∑ ‚Ä¢ ¬∑) (a‚ÇÅ * a‚ÇÇ) : Œ± ‚Üí Œ±) :=
  funext fun _ => (mul_smul _ _ _).symm
#align comp_smul_left comp_smul_left
#align comp_vadd_left comp_vadd_left

variable {M}

/-- Pullback a multiplicative action along an injective map respecting `‚Ä¢`.
See note [reducible non-instances]. -/
@[to_additive (attr := reducible)
    "Pullback an additive action along an injective map respecting `+·µ•`."]
protected def Function.Injective.mulAction [SMul M Œ≤] (f : Œ≤ ‚Üí Œ±) (hf : Injective f)
    (smul : ‚àÄ (c : M) (x), f (c ‚Ä¢ x) = c ‚Ä¢ f x) :
    MulAction M Œ≤ where
  smul := (¬∑ ‚Ä¢ ¬∑)
  one_smul x := hf <| (smul _ _).trans <| one_smul _ (f x)
  mul_smul c‚ÇÅ c‚ÇÇ x := hf <| by simp only [smul, mul_smul]
                               -- üéâ no goals
#align function.injective.mul_action Function.Injective.mulAction
#align function.injective.add_action Function.Injective.addAction

/-- Pushforward a multiplicative action along a surjective map respecting `‚Ä¢`.
See note [reducible non-instances]. -/
@[to_additive (attr := reducible)
    "Pushforward an additive action along a surjective map respecting `+·µ•`."]
protected def Function.Surjective.mulAction [SMul M Œ≤] (f : Œ± ‚Üí Œ≤) (hf : Surjective f)
    (smul : ‚àÄ (c : M) (x), f (c ‚Ä¢ x) = c ‚Ä¢ f x) :
    MulAction M Œ≤ where
  smul := (¬∑ ‚Ä¢ ¬∑)
  one_smul y := by
    rcases hf y with ‚ü®x, rfl‚ü©
    -- ‚ä¢ 1 ‚Ä¢ f x = f x
    rw [‚Üê smul, one_smul]
    -- üéâ no goals
  mul_smul c‚ÇÅ c‚ÇÇ y := by
    rcases hf y with ‚ü®x, rfl‚ü©
    -- ‚ä¢ (c‚ÇÅ * c‚ÇÇ) ‚Ä¢ f x = c‚ÇÅ ‚Ä¢ c‚ÇÇ ‚Ä¢ f x
    simp only [‚Üê smul, mul_smul]
    -- üéâ no goals
#align function.surjective.mul_action Function.Surjective.mulAction
#align function.surjective.add_action Function.Surjective.addAction

/-- Push forward the action of `R` on `M` along a compatible surjective map `f : R ‚Üí* S`.

See also `Function.Surjective.distribMulActionLeft` and `Function.Surjective.moduleLeft`.
-/
@[to_additive (attr := reducible)
    "Push forward the action of `R` on `M` along a compatible surjective map `f : R ‚Üí+ S`."]
def Function.Surjective.mulActionLeft {R S M : Type*} [Monoid R] [MulAction R M] [Monoid S]
    [SMul S M] (f : R ‚Üí* S) (hf : Function.Surjective f)
    (hsmul : ‚àÄ (c) (x : M), f c ‚Ä¢ x = c ‚Ä¢ x) :
    MulAction S M where
  smul := (¬∑ ‚Ä¢ ¬∑)
  one_smul b := by rw [‚Üê f.map_one, hsmul, one_smul]
                   -- üéâ no goals
  mul_smul := hf.forall‚ÇÇ.mpr fun a b x => by simp only [‚Üê f.map_mul, hsmul, mul_smul]
                                             -- üéâ no goals
#align function.surjective.mul_action_left Function.Surjective.mulActionLeft
#align function.surjective.add_action_left Function.Surjective.addActionLeft

section

variable (M)

-- see Note [lower instance priority]
/-- The regular action of a monoid on itself by left multiplication.

This is promoted to a module by `Semiring.toModule`. -/
@[to_additive]
instance (priority := 910) Monoid.toMulAction :
    MulAction M M where
  smul := (¬∑ * ¬∑)
  one_smul := one_mul
  mul_smul := mul_assoc
#align monoid.to_mul_action Monoid.toMulAction
#align add_monoid.to_add_action AddMonoid.toAddAction

/-- The regular action of a monoid on itself by left addition.

This is promoted to an `AddTorsor` by `addGroup_is_addTorsor`. -/
add_decl_doc AddMonoid.toAddAction

@[to_additive]
instance IsScalarTower.left : IsScalarTower M M Œ± :=
  ‚ü®fun x y z => mul_smul x y z‚ü©
#align is_scalar_tower.left IsScalarTower.left
#align vadd_assoc_class.left VAddAssocClass.left

variable {M}

/-- Note that the `IsScalarTower M Œ± Œ±` and `SMulCommClass M Œ± Œ±` typeclass arguments are
usually satisfied by `Algebra M Œ±`. -/
@[to_additive] -- Porting note: nolint to_additive_doc
theorem smul_mul_smul [Mul Œ±] (r s : M) (x y : Œ±) [IsScalarTower M Œ± Œ±] [SMulCommClass M Œ± Œ±] :
    r ‚Ä¢ x * s ‚Ä¢ y = (r * s) ‚Ä¢ (x * y) := by
  rw [smul_mul_assoc, mul_smul_comm, ‚Üê smul_assoc, smul_eq_mul]
  -- üéâ no goals
#align smul_mul_smul smul_mul_smul
#align vadd_add_vadd vadd_add_vadd

end

namespace MulAction

variable (M Œ±)

/-- Embedding of `Œ±` into functions `M ‚Üí Œ±` induced by a multiplicative action of `M` on `Œ±`. -/
@[to_additive]
def toFun : Œ± ‚Ü™ M ‚Üí Œ± :=
  ‚ü®fun y x => x ‚Ä¢ y, fun y‚ÇÅ y‚ÇÇ H => one_smul M y‚ÇÅ ‚ñ∏ one_smul M y‚ÇÇ ‚ñ∏ by convert congr_fun H 1‚ü©
                                                                       -- üéâ no goals
#align mul_action.to_fun MulAction.toFun
#align add_action.to_fun AddAction.toFun

/-- Embedding of `Œ±` into functions `M ‚Üí Œ±` induced by an additive action of `M` on `Œ±`. -/
add_decl_doc AddAction.toFun

variable {M Œ±}

@[to_additive (attr := simp)]
theorem toFun_apply (x : M) (y : Œ±) : MulAction.toFun M Œ± y x = x ‚Ä¢ y :=
  rfl
#align mul_action.to_fun_apply MulAction.toFun_apply
#align add_action.to_fun_apply AddAction.toFun_apply

variable (Œ±)

/-- A multiplicative action of `M` on `Œ±` and a monoid homomorphism `N ‚Üí M` induce
a multiplicative action of `N` on `Œ±`.

See note [reducible non-instances]. -/
@[to_additive (attr := reducible)]
def compHom [Monoid N] (g : N ‚Üí* M) :
    MulAction N Œ± where
  smul := SMul.comp.smul g
  -- Porting note: was `by simp [g.map_one, MulAction.one_smul]`
  one_smul _ := by simp [(¬∑ ‚Ä¢ ¬∑)]; apply MulAction.one_smul
                   -- ‚ä¢ SMul.smul 1 x‚úù = x‚úù
                                   -- üéâ no goals
  -- Porting note: was `by simp [g.map_mul, MulAction.mul_smul]`
  mul_smul _ _ _ := by simp [(¬∑ ‚Ä¢ ¬∑)]; apply MulAction.mul_smul
                       -- ‚ä¢ SMul.smul (‚Üëg x‚úù¬≤ * ‚Üëg x‚úù¬π) x‚úù = SMul.smul (‚Üëg x‚úù¬≤) (SMul.smul (‚Üëg x‚úù¬π) x‚úù)
                                       -- üéâ no goals
#align mul_action.comp_hom MulAction.compHom
#align add_action.comp_hom AddAction.compHom

/-- An additive action of `M` on `Œ±` and an additive monoid homomorphism `N ‚Üí M` induce
an additive action of `N` on `Œ±`.

See note [reducible non-instances]. -/
add_decl_doc AddAction.compHom

end MulAction

end

section CompatibleScalar

@[to_additive]
theorem smul_one_smul {M} (N) [Monoid N] [SMul M N] [MulAction N Œ±] [SMul M Œ±]
    [IsScalarTower M N Œ±] (x : M) (y : Œ±) : (x ‚Ä¢ (1 : N)) ‚Ä¢ y = x ‚Ä¢ y := by
  rw [smul_assoc, one_smul]
  -- üéâ no goals
#align smul_one_smul smul_one_smul
#align vadd_zero_vadd vadd_zero_vadd

@[to_additive (attr := simp)]
theorem smul_one_mul {M N} [MulOneClass N] [SMul M N] [IsScalarTower M N N] (x : M) (y : N) :
    x ‚Ä¢ (1 : N) * y = x ‚Ä¢ y := by rw [smul_mul_assoc, one_mul]
                                  -- üéâ no goals
#align smul_one_mul smul_one_mul
#align vadd_zero_add vadd_zero_add

@[to_additive (attr := simp)]
theorem mul_smul_one {M N} [MulOneClass N] [SMul M N] [SMulCommClass M N N] (x : M) (y : N) :
    y * x ‚Ä¢ (1 : N) = x ‚Ä¢ y := by rw [‚Üê smul_eq_mul, ‚Üê smul_comm, smul_eq_mul, mul_one]
                                  -- üéâ no goals
#align mul_smul_one mul_smul_one
#align add_vadd_zero add_vadd_zero

@[to_additive]
theorem IsScalarTower.of_smul_one_mul {M N} [Monoid N] [SMul M N]
    (h : ‚àÄ (x : M) (y : N), x ‚Ä¢ (1 : N) * y = x ‚Ä¢ y) : IsScalarTower M N N :=
  ‚ü®fun x y z => by rw [‚Üê h, smul_eq_mul, mul_assoc, h, smul_eq_mul]‚ü©
                   -- üéâ no goals
#align is_scalar_tower.of_smul_one_mul IsScalarTower.of_smul_one_mul
#align vadd_assoc_class.of_vadd_zero_add VAddAssocClass.of_vadd_zero_add

@[to_additive]
theorem SMulCommClass.of_mul_smul_one {M N} [Monoid N] [SMul M N]
    (H : ‚àÄ (x : M) (y : N), y * x ‚Ä¢ (1 : N) = x ‚Ä¢ y) : SMulCommClass M N N :=
  ‚ü®fun x y z => by rw [‚Üê H x z, smul_eq_mul, ‚Üê H, smul_eq_mul, mul_assoc]‚ü©
                   -- üéâ no goals
#align smul_comm_class.of_mul_smul_one SMulCommClass.of_mul_smul_one
#align vadd_comm_class.of_add_vadd_zero VAddCommClass.of_add_vadd_zero

/-- If the multiplicative action of `M` on `N` is compatible with multiplication on `N`, then
`fun x => x ‚Ä¢ 1` is a monoid homomorphism from `M` to `N`. -/
@[to_additive (attr := simps)
    "If the additive action of `M` on `N` is compatible with addition on `N`, then
    `fun x => x +·µ• 0` is an additive monoid homomorphism from `M` to `N`."]
def smulOneHom {M N} [Monoid M] [Monoid N] [MulAction M N] [IsScalarTower M N N] :
    M ‚Üí* N where
  toFun x := x ‚Ä¢ (1 : N)
  map_one' := one_smul _ _
  map_mul' x y := by rw [smul_one_mul, smul_smul]
                     -- üéâ no goals
#align smul_one_hom smulOneHom
#align vadd_zero_hom vaddZeroHom
#align smul_one_hom_apply smulOneHom_apply
#align vadd_zero_hom_apply vaddZeroHom_apply

end CompatibleScalar

/-- Typeclass for scalar multiplication that preserves `0` on the right. -/
class SMulZeroClass (M A : Type*) [Zero A] extends SMul M A where
  /-- Multiplying `0` by a scalar gives `0` -/
  smul_zero : ‚àÄ a : M, a ‚Ä¢ (0 : A) = 0
#align smul_zero_class SMulZeroClass

section smul_zero

variable [Zero A] [SMulZeroClass M A]

@[simp]
theorem smul_zero (a : M) : a ‚Ä¢ (0 : A) = 0 :=
  SMulZeroClass.smul_zero _
#align smul_zero smul_zero

/-- Pullback a zero-preserving scalar multiplication along an injective zero-preserving map.
See note [reducible non-instances]. -/
@[reducible]
protected def Function.Injective.smulZeroClass [Zero B] [SMul M B] (f : ZeroHom B A)
    (hf : Injective f) (smul : ‚àÄ (c : M) (x), f (c ‚Ä¢ x) = c ‚Ä¢ f x) :
    SMulZeroClass M B where
  smul := (¬∑ ‚Ä¢ ¬∑)
  smul_zero c := hf <| by simp only [smul, map_zero, smul_zero]
                          -- üéâ no goals
#align function.injective.smul_zero_class Function.Injective.smulZeroClass

/-- Pushforward a zero-preserving scalar multiplication along a zero-preserving map.
See note [reducible non-instances]. -/
@[reducible]
protected def ZeroHom.smulZeroClass [Zero B] [SMul M B] (f : ZeroHom A B)
    (smul : ‚àÄ (c : M) (x), f (c ‚Ä¢ x) = c ‚Ä¢ f x) :
    SMulZeroClass M B where
  -- Porting note: `simp` no longer works here.
  smul_zero c := by rw [‚Üê map_zero f, ‚Üê smul, smul_zero]
                    -- üéâ no goals
#align zero_hom.smul_zero_class ZeroHom.smulZeroClass

/-- Push forward the multiplication of `R` on `M` along a compatible surjective map `f : R ‚Üí S`.

See also `Function.Surjective.distribMulActionLeft`.
-/
@[reducible]
def Function.Surjective.smulZeroClassLeft {R S M : Type*} [Zero M] [SMulZeroClass R M]
    [SMul S M] (f : R ‚Üí S) (hf : Function.Surjective f)
    (hsmul : ‚àÄ (c) (x : M), f c ‚Ä¢ x = c ‚Ä¢ x) :
    SMulZeroClass S M where
  smul := (¬∑ ‚Ä¢ ¬∑)
  smul_zero := hf.forall.mpr fun c => by rw [hsmul, smul_zero]
                                         -- üéâ no goals
#align function.surjective.smul_zero_class_left Function.Surjective.smulZeroClassLeft

variable (A)

/-- Compose a `SMulZeroClass` with a function, with scalar multiplication `f r' ‚Ä¢ m`.
See note [reducible non-instances]. -/
@[reducible]
def SMulZeroClass.compFun (f : N ‚Üí M) :
    SMulZeroClass N A where
  smul := SMul.comp.smul f
  smul_zero x := smul_zero (f x)
#align smul_zero_class.comp_fun SMulZeroClass.compFun

/-- Each element of the scalars defines a zero-preserving map. -/
@[simps]
def SMulZeroClass.toZeroHom (x : M) :
    ZeroHom A A where
  toFun := (¬∑ ‚Ä¢ ¬∑) x
  map_zero' := smul_zero x
#align smul_zero_class.to_zero_hom SMulZeroClass.toZeroHom
#align smul_zero_class.to_zero_hom_apply SMulZeroClass.toZeroHom_apply

end smul_zero

/-- Typeclass for scalar multiplication that preserves `0` and `+` on the right.

This is exactly `DistribMulAction` without the `MulAction` part.
-/
@[ext]
class DistribSMul (M A : Type*) [AddZeroClass A] extends SMulZeroClass M A where
  /-- Scalar multiplication distributes across addition -/
  smul_add : ‚àÄ (a : M) (x y : A), a ‚Ä¢ (x + y) = a ‚Ä¢ x + a ‚Ä¢ y
#align distrib_smul DistribSMul
#align distrib_smul.ext DistribSMul.ext
#align distrib_smul.ext_iff DistribSMul.ext_iff

section DistribSMul

variable [AddZeroClass A] [DistribSMul M A]

theorem smul_add (a : M) (b‚ÇÅ b‚ÇÇ : A) : a ‚Ä¢ (b‚ÇÅ + b‚ÇÇ) = a ‚Ä¢ b‚ÇÅ + a ‚Ä¢ b‚ÇÇ :=
  DistribSMul.smul_add _ _ _
#align smul_add smul_add

instance AddMonoidHom.smulZeroClass [AddZeroClass B] : SMulZeroClass M (B ‚Üí+ A) where
  smul r f :=
    { toFun := (fun a => r ‚Ä¢ (f a))
      map_zero' := by simp only [map_zero, smul_zero]
                      -- üéâ no goals
      map_add' := fun x y => by simp only [map_add, smul_add] }
                                -- üéâ no goals
  smul_zero r := ext fun _ => smul_zero _

/-- Pullback a distributive scalar multiplication along an injective additive monoid
homomorphism.
See note [reducible non-instances]. -/
@[reducible]
protected def Function.Injective.distribSMul [AddZeroClass B] [SMul M B] (f : B ‚Üí+ A)
    (hf : Injective f) (smul : ‚àÄ (c : M) (x), f (c ‚Ä¢ x) = c ‚Ä¢ f x) : DistribSMul M B :=
  { hf.smulZeroClass f.toZeroHom smul with
    smul := (¬∑ ‚Ä¢ ¬∑),
    smul_add := fun c x y => hf <| by simp only [smul, map_add, smul_add] }
                                      -- üéâ no goals
#align function.injective.distrib_smul Function.Injective.distribSMul

/-- Pushforward a distributive scalar multiplication along a surjective additive monoid
homomorphism.
See note [reducible non-instances]. -/
@[reducible]
protected def Function.Surjective.distribSMul [AddZeroClass B] [SMul M B] (f : A ‚Üí+ B)
    (hf : Surjective f) (smul : ‚àÄ (c : M) (x), f (c ‚Ä¢ x) = c ‚Ä¢ f x) : DistribSMul M B :=
  { f.toZeroHom.smulZeroClass smul with
    smul := (¬∑ ‚Ä¢ ¬∑),
    smul_add := fun c x y => by
      rcases hf x with ‚ü®x, rfl‚ü©
      -- ‚ä¢ c ‚Ä¢ (‚Üëf x + y) = c ‚Ä¢ ‚Üëf x + c ‚Ä¢ y
      rcases hf y with ‚ü®y, rfl‚ü©
      -- ‚ä¢ c ‚Ä¢ (‚Üëf x + ‚Üëf y) = c ‚Ä¢ ‚Üëf x + c ‚Ä¢ ‚Üëf y
      simp only [smul_add, ‚Üê smul, ‚Üê map_add] }
      -- üéâ no goals
#align function.surjective.distrib_smul Function.Surjective.distribSMul

/-- Push forward the multiplication of `R` on `M` along a compatible surjective map `f : R ‚Üí S`.

See also `Function.Surjective.distribMulActionLeft`.
-/
@[reducible]
def Function.Surjective.distribSMulLeft {R S M : Type*} [AddZeroClass M] [DistribSMul R M]
    [SMul S M] (f : R ‚Üí S) (hf : Function.Surjective f)
    (hsmul : ‚àÄ (c) (x : M), f c ‚Ä¢ x = c ‚Ä¢ x) : DistribSMul S M :=
  { hf.smulZeroClassLeft f hsmul with
    smul := (¬∑ ‚Ä¢ ¬∑),
    smul_add := hf.forall.mpr fun c x y => by simp only [hsmul, smul_add] }
                                              -- üéâ no goals
#align function.surjective.distrib_smul_left Function.Surjective.distribSMulLeft

variable (A)

/-- Compose a `DistribSMul` with a function, with scalar multiplication `f r' ‚Ä¢ m`.
See note [reducible non-instances]. -/
@[reducible]
def DistribSMul.compFun (f : N ‚Üí M) : DistribSMul N A :=
  { SMulZeroClass.compFun A f with
    smul := SMul.comp.smul f,
    smul_add := fun x => smul_add (f x) }
#align distrib_smul.comp_fun DistribSMul.compFun

/-- Each element of the scalars defines an additive monoid homomorphism. -/
@[simps]
def DistribSMul.toAddMonoidHom (x : M) : A ‚Üí+ A :=
  { SMulZeroClass.toZeroHom A x with toFun := (¬∑ ‚Ä¢ ¬∑) x, map_add' := smul_add x }
#align distrib_smul.to_add_monoid_hom DistribSMul.toAddMonoidHom
#align distrib_smul.to_add_monoid_hom_apply DistribSMul.toAddMonoidHom_apply

end DistribSMul

/-- Typeclass for multiplicative actions on additive structures. This generalizes group modules. -/
@[ext]
class DistribMulAction (M A : Type*) [Monoid M] [AddMonoid A] extends MulAction M A where
  /-- Multiplying `0` by a scalar gives `0` -/
  smul_zero : ‚àÄ a : M, a ‚Ä¢ (0 : A) = 0
  /-- Scalar multiplication distributes across addition -/
  smul_add : ‚àÄ (a : M) (x y : A), a ‚Ä¢ (x + y) = a ‚Ä¢ x + a ‚Ä¢ y
#align distrib_mul_action DistribMulAction
#align distrib_mul_action.ext DistribMulAction.ext
#align distrib_mul_action.ext_iff DistribMulAction.ext_iff

section

variable [Monoid M] [AddMonoid A] [DistribMulAction M A]

-- See note [lower instance priority]
instance (priority := 100) DistribMulAction.toDistribSMul : DistribSMul M A :=
  { ‚ÄπDistribMulAction M A‚Ä∫ with }
#align distrib_mul_action.to_distrib_smul DistribMulAction.toDistribSMul

-- Porting note: this probably is no longer relevant.
/-! Since Lean 3 does not have definitional eta for structures, we have to make sure
that the definition of `DistribMulAction.toDistribSMul` was done correctly,
and the two paths from `DistribMulAction` to `SMul` are indeed definitionally equal. -/
example :
    (DistribMulAction.toMulAction.toSMul : SMul M A) =
      DistribMulAction.toDistribSMul.toSMul :=
  rfl

/-- Pullback a distributive multiplicative action along an injective additive monoid
homomorphism.
See note [reducible non-instances]. -/
@[reducible]
protected def Function.Injective.distribMulAction [AddMonoid B] [SMul M B] (f : B ‚Üí+ A)
    (hf : Injective f) (smul : ‚àÄ (c : M) (x), f (c ‚Ä¢ x) = c ‚Ä¢ f x) : DistribMulAction M B :=
  { hf.distribSMul f smul, hf.mulAction f smul with smul := (¬∑ ‚Ä¢ ¬∑) }
#align function.injective.distrib_mul_action Function.Injective.distribMulAction

/-- Pushforward a distributive multiplicative action along a surjective additive monoid
homomorphism.
See note [reducible non-instances]. -/
@[reducible]
protected def Function.Surjective.distribMulAction [AddMonoid B] [SMul M B] (f : A ‚Üí+ B)
    (hf : Surjective f) (smul : ‚àÄ (c : M) (x), f (c ‚Ä¢ x) = c ‚Ä¢ f x) : DistribMulAction M B :=
  { hf.distribSMul f smul, hf.mulAction f smul with smul := (¬∑ ‚Ä¢ ¬∑) }
#align function.surjective.distrib_mul_action Function.Surjective.distribMulAction

/-- Push forward the action of `R` on `M` along a compatible surjective map `f : R ‚Üí* S`.

See also `Function.Surjective.mulActionLeft` and `Function.Surjective.moduleLeft`.
-/
@[reducible]
def Function.Surjective.distribMulActionLeft {R S M : Type*} [Monoid R] [AddMonoid M]
    [DistribMulAction R M] [Monoid S] [SMul S M] (f : R ‚Üí* S) (hf : Function.Surjective f)
    (hsmul : ‚àÄ (c) (x : M), f c ‚Ä¢ x = c ‚Ä¢ x) : DistribMulAction S M :=
  { hf.distribSMulLeft f hsmul, hf.mulActionLeft f hsmul with smul := (¬∑ ‚Ä¢ ¬∑) }
#align function.surjective.distrib_mul_action_left Function.Surjective.distribMulActionLeft

variable (A)

/-- Compose a `DistribMulAction` with a `MonoidHom`, with action `f r' ‚Ä¢ m`.
See note [reducible non-instances]. -/
@[reducible]
def DistribMulAction.compHom [Monoid N] (f : N ‚Üí* M) : DistribMulAction N A :=
  { DistribSMul.compFun A f, MulAction.compHom A f with smul := SMul.comp.smul f }
#align distrib_mul_action.comp_hom DistribMulAction.compHom

/-- Each element of the monoid defines an additive monoid homomorphism. -/
@[simps!]
def DistribMulAction.toAddMonoidHom (x : M) : A ‚Üí+ A :=
  DistribSMul.toAddMonoidHom A x
#align distrib_mul_action.to_add_monoid_hom DistribMulAction.toAddMonoidHom
#align distrib_mul_action.to_add_monoid_hom_apply DistribMulAction.toAddMonoidHom_apply

variable (M)

/-- Each element of the monoid defines an additive monoid homomorphism. -/
@[simps]
def DistribMulAction.toAddMonoidEnd :
    M ‚Üí* AddMonoid.End A where
  toFun := DistribMulAction.toAddMonoidHom A
  map_one' := AddMonoidHom.ext <| one_smul M
  map_mul' x y := AddMonoidHom.ext <| mul_smul x y
#align distrib_mul_action.to_add_monoid_End DistribMulAction.toAddMonoidEnd
#align distrib_mul_action.to_add_monoid_End_apply DistribMulAction.toAddMonoidEnd_apply

instance AddMonoid.nat_smulCommClass :
    SMulCommClass ‚Ñï M
      A where smul_comm n x y := ((DistribMulAction.toAddMonoidHom A x).map_nsmul y n).symm
#align add_monoid.nat_smul_comm_class AddMonoid.nat_smulCommClass

-- `SMulCommClass.symm` is not registered as an instance, as it would cause a loop
instance AddMonoid.nat_smulCommClass' : SMulCommClass M ‚Ñï A :=
  SMulCommClass.symm _ _ _
#align add_monoid.nat_smul_comm_class' AddMonoid.nat_smulCommClass'

end

section

variable [Monoid M] [AddGroup A] [DistribMulAction M A]

instance AddGroup.int_smulCommClass : SMulCommClass ‚Ñ§ M A where
  smul_comm n x y := ((DistribMulAction.toAddMonoidHom A x).map_zsmul y n).symm
#align add_group.int_smul_comm_class AddGroup.int_smulCommClass

-- `SMulCommClass.symm` is not registered as an instance, as it would cause a loop
instance AddGroup.int_smulCommClass' : SMulCommClass M ‚Ñ§ A :=
  SMulCommClass.symm _ _ _
#align add_group.int_smul_comm_class' AddGroup.int_smulCommClass'

@[simp]
theorem smul_neg (r : M) (x : A) : r ‚Ä¢ -x = -(r ‚Ä¢ x) :=
  eq_neg_of_add_eq_zero_left <| by rw [‚Üê smul_add, neg_add_self, smul_zero]
                                   -- üéâ no goals
#align smul_neg smul_neg

theorem smul_sub (r : M) (x y : A) : r ‚Ä¢ (x - y) = r ‚Ä¢ x - r ‚Ä¢ y := by
  rw [sub_eq_add_neg, sub_eq_add_neg, smul_add, smul_neg]
  -- üéâ no goals
#align smul_sub smul_sub

end

/-- Typeclass for multiplicative actions on multiplicative structures. This generalizes
conjugation actions. -/
@[ext]
class MulDistribMulAction (M : Type*) (A : Type*) [Monoid M] [Monoid A] extends
  MulAction M A where
  /-- Distributivity of `‚Ä¢` across `*` -/
  smul_mul : ‚àÄ (r : M) (x y : A), r ‚Ä¢ (x * y) = r ‚Ä¢ x * r ‚Ä¢ y
  /-- Multiplying `1` by a scalar gives `1` -/
  smul_one : ‚àÄ r : M, r ‚Ä¢ (1 : A) = 1
#align mul_distrib_mul_action MulDistribMulAction
#align mul_distrib_mul_action.ext MulDistribMulAction.ext
#align mul_distrib_mul_action.ext_iff MulDistribMulAction.ext_iff

export MulDistribMulAction (smul_one)

section

variable [Monoid M] [Monoid A] [MulDistribMulAction M A]

theorem smul_mul' (a : M) (b‚ÇÅ b‚ÇÇ : A) : a ‚Ä¢ (b‚ÇÅ * b‚ÇÇ) = a ‚Ä¢ b‚ÇÅ * a ‚Ä¢ b‚ÇÇ :=
  MulDistribMulAction.smul_mul _ _ _
#align smul_mul' smul_mul'

/-- Pullback a multiplicative distributive multiplicative action along an injective monoid
homomorphism.
See note [reducible non-instances]. -/
@[reducible]
protected def Function.Injective.mulDistribMulAction [Monoid B] [SMul M B] (f : B ‚Üí* A)
    (hf : Injective f) (smul : ‚àÄ (c : M) (x), f (c ‚Ä¢ x) = c ‚Ä¢ f x) : MulDistribMulAction M B :=
  { hf.mulAction f smul with
    smul := (¬∑ ‚Ä¢ ¬∑),
    smul_mul := fun c x y => hf <| by simp only [smul, f.map_mul, smul_mul'],
                                      -- üéâ no goals
    smul_one := fun c => hf <| by simp only [smul, f.map_one, smul_one] }
                                  -- üéâ no goals
#align function.injective.mul_distrib_mul_action Function.Injective.mulDistribMulAction

/-- Pushforward a multiplicative distributive multiplicative action along a surjective monoid
homomorphism.
See note [reducible non-instances]. -/
@[reducible]
protected def Function.Surjective.mulDistribMulAction [Monoid B] [SMul M B] (f : A ‚Üí* B)
    (hf : Surjective f) (smul : ‚àÄ (c : M) (x), f (c ‚Ä¢ x) = c ‚Ä¢ f x) : MulDistribMulAction M B :=
  { hf.mulAction f smul with
    smul := (¬∑ ‚Ä¢ ¬∑),
    smul_mul := fun c x y => by
      rcases hf x with ‚ü®x, rfl‚ü©
      -- ‚ä¢ c ‚Ä¢ (‚Üëf x * y) = c ‚Ä¢ ‚Üëf x * c ‚Ä¢ y
      rcases hf y with ‚ü®y, rfl‚ü©
      -- ‚ä¢ c ‚Ä¢ (‚Üëf x * ‚Üëf y) = c ‚Ä¢ ‚Üëf x * c ‚Ä¢ ‚Üëf y
      simp only [smul_mul', ‚Üê smul, ‚Üê f.map_mul],
      -- üéâ no goals
    smul_one := fun c => by rw [‚Üê f.map_one, ‚Üê smul, smul_one] }
                            -- üéâ no goals
#align function.surjective.mul_distrib_mul_action Function.Surjective.mulDistribMulAction

variable (A)

/-- Compose a `MulDistribMulAction` with a `MonoidHom`, with action `f r' ‚Ä¢ m`.
See note [reducible non-instances]. -/
@[reducible]
def MulDistribMulAction.compHom [Monoid N] (f : N ‚Üí* M) : MulDistribMulAction N A :=
  { MulAction.compHom A f with
    smul := SMul.comp.smul f,
    smul_one := fun x => smul_one (f x),
    smul_mul := fun x => smul_mul' (f x) }
#align mul_distrib_mul_action.comp_hom MulDistribMulAction.compHom

/-- Scalar multiplication by `r` as a `MonoidHom`. -/
def MulDistribMulAction.toMonoidHom (r : M) :
    A ‚Üí* A where
  toFun := (¬∑ ‚Ä¢ ¬∑) r
  map_one' := smul_one r
  map_mul' := smul_mul' r
#align mul_distrib_mul_action.to_monoid_hom MulDistribMulAction.toMonoidHom

variable {A}

@[simp]
theorem MulDistribMulAction.toMonoidHom_apply (r : M) (x : A) :
    MulDistribMulAction.toMonoidHom A r x = r ‚Ä¢ x :=
  rfl
#align mul_distrib_mul_action.to_monoid_hom_apply MulDistribMulAction.toMonoidHom_apply

variable (M A)

/-- Each element of the monoid defines a monoid homomorphism. -/
@[simps]
def MulDistribMulAction.toMonoidEnd :
    M ‚Üí* Monoid.End A where
  toFun := MulDistribMulAction.toMonoidHom A
  map_one' := MonoidHom.ext <| one_smul M
  map_mul' x y := MonoidHom.ext <| mul_smul x y
#align mul_distrib_mul_action.to_monoid_End MulDistribMulAction.toMonoidEnd
#align mul_distrib_mul_action.to_monoid_End_apply MulDistribMulAction.toMonoidEnd_apply

end

section

variable [Monoid M] [Group A] [MulDistribMulAction M A]

@[simp]
theorem smul_inv' (r : M) (x : A) : r ‚Ä¢ x‚Åª¬π = (r ‚Ä¢ x)‚Åª¬π :=
  (MulDistribMulAction.toMonoidHom A r).map_inv x
#align smul_inv' smul_inv'

theorem smul_div' (r : M) (x y : A) : r ‚Ä¢ (x / y) = r ‚Ä¢ x / r ‚Ä¢ y :=
  map_div (MulDistribMulAction.toMonoidHom A r) x y
#align smul_div' smul_div'

end

variable (Œ±)

/-- The monoid of endomorphisms.

Note that this is generalized by `CategoryTheory.End` to categories other than `Type u`. -/
protected def Function.End :=
  Œ± ‚Üí Œ±
#align function.End Function.End

instance : Monoid (Function.End Œ±) where
  one := id
  mul := (¬∑ ‚àò ¬∑)
  mul_assoc f g h := rfl
  mul_one f := rfl
  one_mul f := rfl

instance : Inhabited (Function.End Œ±) :=
  ‚ü®1‚ü©

variable {Œ±}

/-- The tautological action by `Function.End Œ±` on `Œ±`.

This is generalized to bundled endomorphisms by:
* `Equiv.Perm.applyMulAction`
* `AddMonoid.End.applyDistribMulAction`
* `AddAut.applyDistribMulAction`
* `MulAut.applyMulDistribMulAction`
* `RingHom.applyDistribMulAction`
* `LinearEquiv.applyDistribMulAction`
* `LinearMap.applyModule`
* `RingHom.applyMulSemiringAction`
* `AlgEquiv.applyMulSemiringAction`
-/
instance Function.End.applyMulAction :
    MulAction (Function.End Œ±) Œ± where
  smul := (¬∑ <| ¬∑)
  one_smul _ := rfl
  mul_smul _ _ _ := rfl
#align function.End.apply_mul_action Function.End.applyMulAction

@[simp]
theorem Function.End.smul_def (f : Function.End Œ±) (a : Œ±) : f ‚Ä¢ a = f a :=
  rfl
#align function.End.smul_def Function.End.smul_def

--TODO - This statement should be somethting like `toFun (f * g) = toFun f ‚àò toFun g`
theorem Function.End.mul_def (f g : Function.End Œ±) : (f * g) = f ‚àò g :=
  rfl

--TODO - This statement should be somethting like `toFun 1 = id`
theorem Function.End.one_def : (1 : Function.End Œ±) = id :=
  rfl

/-- `Function.End.applyMulAction` is faithful. -/
instance Function.End.apply_FaithfulSMul : FaithfulSMul (Function.End Œ±) Œ± :=
  ‚ü®fun {_ _} => funext‚ü©
#align function.End.apply_has_faithful_smul Function.End.apply_FaithfulSMul

/-- The tautological action by `AddMonoid.End Œ±` on `Œ±`.

This generalizes `Function.End.applyMulAction`. -/
instance AddMonoid.End.applyDistribMulAction [AddMonoid Œ±] :
    DistribMulAction (AddMonoid.End Œ±) Œ± where
  smul := (¬∑ <| ¬∑)
  smul_zero := AddMonoidHom.map_zero
  smul_add := AddMonoidHom.map_add
  one_smul _ := rfl
  mul_smul _ _ _ := rfl
#align add_monoid.End.apply_distrib_mul_action AddMonoid.End.applyDistribMulAction

@[simp]
theorem AddMonoid.End.smul_def [AddMonoid Œ±] (f : AddMonoid.End Œ±) (a : Œ±) : f ‚Ä¢ a = f a :=
  rfl
#align add_monoid.End.smul_def AddMonoid.End.smul_def

/-- `AddMonoid.End.applyDistribMulAction` is faithful. -/
instance AddMonoid.End.applyFaithfulSMul [AddMonoid Œ±] :
    FaithfulSMul (AddMonoid.End Œ±) Œ± :=
  ‚ü®fun {_ _ h} => AddMonoidHom.ext h‚ü©
#align add_monoid.End.apply_has_faithful_smul AddMonoid.End.applyFaithfulSMul

/-- The monoid hom representing a monoid action.

When `M` is a group, see `MulAction.toPermHom`. -/
def MulAction.toEndHom [Monoid M] [MulAction M Œ±] : M ‚Üí* Function.End Œ± where
  toFun := (¬∑ ‚Ä¢ ¬∑)
  map_one' := funext (one_smul M)
  map_mul' x y := funext (mul_smul x y)
#align mul_action.to_End_hom MulAction.toEndHom

/-- The monoid action induced by a monoid hom to `Function.End Œ±`

See note [reducible non-instances]. -/
@[reducible]
def MulAction.ofEndHom [Monoid M] (f : M ‚Üí* Function.End Œ±) : MulAction M Œ± :=
  MulAction.compHom Œ± f
#align mul_action.of_End_hom MulAction.ofEndHom

/-! ### `Additive`, `Multiplicative` -/

section

open Additive Multiplicative

instance Additive.vadd [SMul Œ± Œ≤] : VAdd (Additive Œ±) Œ≤ :=
  ‚ü®fun a => (toMul a ‚Ä¢ ¬∑)‚ü©
#align additive.has_vadd Additive.vadd

instance Multiplicative.smul [VAdd Œ± Œ≤] : SMul (Multiplicative Œ±) Œ≤ :=
  ‚ü®fun a => (toAdd a +·µ• ¬∑)‚ü©
#align multiplicative.has_smul Multiplicative.smul

@[simp]
theorem toMul_smul [SMul Œ± Œ≤] (a) (b : Œ≤) : (toMul a : Œ±) ‚Ä¢ b = a +·µ• b :=
  rfl
#align to_mul_smul toMul_smul

@[simp]
theorem ofMul_vadd [SMul Œ± Œ≤] (a : Œ±) (b : Œ≤) : ofMul a +·µ• b = a ‚Ä¢ b :=
  rfl
#align of_mul_vadd ofMul_vadd

@[simp]
theorem toAdd_vadd [VAdd Œ± Œ≤] (a) (b : Œ≤) : (toAdd a : Œ±) +·µ• b = a ‚Ä¢ b :=
  rfl
#align to_add_vadd toAdd_vadd

@[simp]
theorem ofAdd_smul [VAdd Œ± Œ≤] (a : Œ±) (b : Œ≤) : ofAdd a ‚Ä¢ b = a +·µ• b :=
  rfl
#align of_add_smul ofAdd_smul

-- Porting note: I don't know why `one_smul` can do without an explicit Œ± and `mul_smul` can't.
instance Additive.addAction [Monoid Œ±] [MulAction Œ± Œ≤] :
    AddAction (Additive Œ±) Œ≤ where
  zero_vadd := MulAction.one_smul
  add_vadd := @MulAction.mul_smul Œ± _ _ _
#align additive.add_action Additive.addAction

instance Multiplicative.mulAction [AddMonoid Œ±] [AddAction Œ± Œ≤] :
    MulAction (Multiplicative Œ±)
      Œ≤ where
  one_smul := AddAction.zero_vadd
  mul_smul := @AddAction.add_vadd Œ± _ _ _
#align multiplicative.mul_action Multiplicative.mulAction

instance Additive.addAction_isPretransitive [Monoid Œ±] [MulAction Œ± Œ≤]
    [MulAction.IsPretransitive Œ± Œ≤] : AddAction.IsPretransitive (Additive Œ±) Œ≤ :=
  ‚ü®@MulAction.exists_smul_eq Œ± _ _ _‚ü©
#align additive.add_action_is_pretransitive Additive.addAction_isPretransitive

instance Multiplicative.mulAction_isPretransitive [AddMonoid Œ±] [AddAction Œ± Œ≤]
    [AddAction.IsPretransitive Œ± Œ≤] : MulAction.IsPretransitive (Multiplicative Œ±) Œ≤ :=
  ‚ü®@AddAction.exists_vadd_eq Œ± _ _ _‚ü©
#align multiplicative.add_action_is_pretransitive Multiplicative.mulAction_isPretransitive

instance Additive.vaddCommClass [SMul Œ± Œ≥] [SMul Œ≤ Œ≥] [SMulCommClass Œ± Œ≤ Œ≥] :
    VAddCommClass (Additive Œ±) (Additive Œ≤) Œ≥ :=
  ‚ü®@smul_comm Œ± Œ≤ _ _ _ _‚ü©
#align additive.vadd_comm_class Additive.vaddCommClass

instance Multiplicative.smulCommClass [VAdd Œ± Œ≥] [VAdd Œ≤ Œ≥] [VAddCommClass Œ± Œ≤ Œ≥] :
    SMulCommClass (Multiplicative Œ±) (Multiplicative Œ≤) Œ≥ :=
  ‚ü®@vadd_comm Œ± Œ≤ _ _ _ _‚ü©
#align multiplicative.smul_comm_class Multiplicative.smulCommClass

end

/-- The tautological additive action by `Additive (Function.End Œ±)` on `Œ±`. -/
instance AddAction.functionEnd : AddAction (Additive (Function.End Œ±)) Œ± :=
  inferInstance
#align add_action.function_End AddAction.functionEnd

/-- The additive monoid hom representing an additive monoid action.

When `M` is a group, see `AddAction.toPermHom`. -/
def AddAction.toEndHom [AddMonoid M] [AddAction M Œ±] : M ‚Üí+ Additive (Function.End Œ±) :=
  MonoidHom.toAdditive'' MulAction.toEndHom
#align add_action.to_End_hom AddAction.toEndHom

/-- The additive action induced by a hom to `Additive (Function.End Œ±)`

See note [reducible non-instances]. -/
@[reducible]
def AddAction.ofEndHom [AddMonoid M] (f : M ‚Üí+ Additive (Function.End Œ±)) : AddAction M Œ± :=
  AddAction.compHom Œ± f
#align add_action.of_End_hom AddAction.ofEndHom
