/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes
-/
import Mathlib.Algebra.Hom.Aut
import Mathlib.GroupTheory.GroupAction.Units

#align_import group_theory.group_action.group from "leanprover-community/mathlib"@"ba2245edf0c8bb155f1569fd9b9492a9b384cde6"

/-!
# Group actions applied to various types of group

This file contains lemmas about `SMul` on `GroupWithZero`, and `Group`.
-/


universe u v w

variable {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w}

section MulAction

/-- `Monoid.toMulAction` is faithful on cancellative monoids. -/
@[to_additive " `AddMonoid.toAddAction` is faithful on additive cancellative monoids. "]
instance RightCancelMonoid.faithfulSMul [RightCancelMonoid Œ±] : FaithfulSMul Œ± Œ± :=
  ‚ü®fun h => mul_right_cancel (h 1)‚ü©
#align right_cancel_monoid.to_has_faithful_smul RightCancelMonoid.faithfulSMul
#align add_right_cancel_monoid.to_has_faithful_vadd AddRightCancelMonoid.faithfulVAdd

section Group

variable [Group Œ±] [MulAction Œ± Œ≤]

@[to_additive (attr := simp)]
theorem inv_smul_smul (c : Œ±) (x : Œ≤) : c‚Åª¬π ‚Ä¢ c ‚Ä¢ x = x := by rw [smul_smul, mul_left_inv, one_smul]
                                                              -- üéâ no goals
#align inv_smul_smul inv_smul_smul
#align neg_vadd_vadd neg_vadd_vadd

@[to_additive (attr := simp)]
theorem smul_inv_smul (c : Œ±) (x : Œ≤) : c ‚Ä¢ c‚Åª¬π ‚Ä¢ x = x := by
  rw [smul_smul, mul_right_inv, one_smul]
  -- üéâ no goals
#align smul_inv_smul smul_inv_smul
#align vadd_neg_vadd vadd_neg_vadd

/-- Given an action of a group `Œ±` on `Œ≤`, each `g : Œ±` defines a permutation of `Œ≤`. -/
@[to_additive (attr := simps)]
def MulAction.toPerm (a : Œ±) : Equiv.Perm Œ≤ :=
  ‚ü®fun x => a ‚Ä¢ x, fun x => a‚Åª¬π ‚Ä¢ x, inv_smul_smul a, smul_inv_smul a‚ü©
#align mul_action.to_perm MulAction.toPerm
#align add_action.to_perm AddAction.toPerm
#align add_action.to_perm_apply AddAction.toPerm_apply
#align mul_action.to_perm_apply MulAction.toPerm_apply
#align add_action.to_perm_symm_apply AddAction.toPerm_symm_apply
#align mul_action.to_perm_symm_apply MulAction.toPerm_symm_apply

/-- Given an action of an additive group `Œ±` on `Œ≤`, each `g : Œ±` defines a permutation of `Œ≤`. -/
add_decl_doc AddAction.toPerm

/-- `MulAction.toPerm` is injective on faithful actions. -/
@[to_additive "`AddAction.toPerm` is injective on faithful actions."]
theorem MulAction.toPerm_injective [FaithfulSMul Œ± Œ≤] :
    Function.Injective (MulAction.toPerm : Œ± ‚Üí Equiv.Perm Œ≤) :=
  (show Function.Injective (Equiv.toFun ‚àò MulAction.toPerm) from smul_left_injective').of_comp
#align mul_action.to_perm_injective MulAction.toPerm_injective
#align add_action.to_perm_injective AddAction.toPerm_injective

variable (Œ±) (Œ≤)

/-- Given an action of a group `Œ±` on a set `Œ≤`, each `g : Œ±` defines a permutation of `Œ≤`. -/
@[simps]
def MulAction.toPermHom : Œ± ‚Üí* Equiv.Perm Œ≤ where
  toFun := MulAction.toPerm
  map_one' := Equiv.ext <| one_smul Œ±
  map_mul' u‚ÇÅ u‚ÇÇ := Equiv.ext <| mul_smul (u‚ÇÅ : Œ±) u‚ÇÇ
#align mul_action.to_perm_hom MulAction.toPermHom
#align mul_action.to_perm_hom_apply MulAction.toPermHom_apply

/-- Given an action of an additive group `Œ±` on a set `Œ≤`, each `g : Œ±` defines a permutation of
`Œ≤`. -/
@[simps!]
def AddAction.toPermHom (Œ± : Type*) [AddGroup Œ±] [AddAction Œ± Œ≤] :
    Œ± ‚Üí+ Additive (Equiv.Perm Œ≤) :=
  MonoidHom.toAdditive'' <| MulAction.toPermHom (Multiplicative Œ±) Œ≤
#align add_action.to_perm_hom AddAction.toPermHom

/-- The tautological action by `Equiv.Perm Œ±` on `Œ±`.

This generalizes `Function.End.applyMulAction`.-/
instance Equiv.Perm.applyMulAction (Œ± : Type*) : MulAction (Equiv.Perm Œ±) Œ± where
  smul f a := f a
  one_smul _ := rfl
  mul_smul _ _ _ := rfl
#align equiv.perm.apply_mul_action Equiv.Perm.applyMulAction

@[simp]
protected theorem Equiv.Perm.smul_def {Œ± : Type*} (f : Equiv.Perm Œ±) (a : Œ±) : f ‚Ä¢ a = f a :=
  rfl
#align equiv.perm.smul_def Equiv.Perm.smul_def

/-- `Equiv.Perm.applyMulAction` is faithful. -/
instance Equiv.Perm.applyFaithfulSMul (Œ± : Type*) : FaithfulSMul (Equiv.Perm Œ±) Œ± :=
  ‚ü®Equiv.ext‚ü©
#align equiv.perm.apply_has_faithful_smul Equiv.Perm.applyFaithfulSMul

variable {Œ±} {Œ≤}

@[to_additive]
theorem inv_smul_eq_iff {a : Œ±} {x y : Œ≤} : a‚Åª¬π ‚Ä¢ x = y ‚Üî x = a ‚Ä¢ y :=
  (MulAction.toPerm a).symm_apply_eq
#align inv_smul_eq_iff inv_smul_eq_iff
#align neg_vadd_eq_iff neg_vadd_eq_iff

@[to_additive]
theorem eq_inv_smul_iff {a : Œ±} {x y : Œ≤} : x = a‚Åª¬π ‚Ä¢ y ‚Üî a ‚Ä¢ x = y :=
  (MulAction.toPerm a).eq_symm_apply
#align eq_inv_smul_iff eq_inv_smul_iff
#align eq_neg_vadd_iff eq_neg_vadd_iff

theorem smul_inv [Group Œ≤] [SMulCommClass Œ± Œ≤ Œ≤] [IsScalarTower Œ± Œ≤ Œ≤] (c : Œ±) (x : Œ≤) :
    (c ‚Ä¢ x)‚Åª¬π = c‚Åª¬π ‚Ä¢ x‚Åª¬π := by
  rw [inv_eq_iff_mul_eq_one, smul_mul_smul, mul_right_inv, mul_right_inv, one_smul]
  -- üéâ no goals
#align smul_inv smul_inv

theorem smul_zpow [Group Œ≤] [SMulCommClass Œ± Œ≤ Œ≤] [IsScalarTower Œ± Œ≤ Œ≤] (c : Œ±) (x : Œ≤) (p : ‚Ñ§) :
    (c ‚Ä¢ x) ^ p = c ^ p ‚Ä¢ x ^ p := by
  cases p <;>
  -- ‚ä¢ (c ‚Ä¢ x) ^ Int.ofNat a‚úù = c ^ Int.ofNat a‚úù ‚Ä¢ x ^ Int.ofNat a‚úù
  simp [smul_pow, smul_inv]
  -- üéâ no goals
  -- üéâ no goals
#align smul_zpow smul_zpow

@[simp]
theorem Commute.smul_right_iff [Mul Œ≤] [SMulCommClass Œ± Œ≤ Œ≤] [IsScalarTower Œ± Œ≤ Œ≤] {a b : Œ≤}
    (r : Œ±) : Commute a (r ‚Ä¢ b) ‚Üî Commute a b :=
  ‚ü®fun h => inv_smul_smul r b ‚ñ∏ h.smul_right r‚Åª¬π, fun h => h.smul_right r‚ü©
#align commute.smul_right_iff Commute.smul_right_iff

@[simp]
theorem Commute.smul_left_iff [Mul Œ≤] [SMulCommClass Œ± Œ≤ Œ≤] [IsScalarTower Œ± Œ≤ Œ≤] {a b : Œ≤}
    (r : Œ±) : Commute (r ‚Ä¢ a) b ‚Üî Commute a b := by
  rw [Commute.symm_iff, Commute.smul_right_iff, Commute.symm_iff]
  -- üéâ no goals
#align commute.smul_left_iff Commute.smul_left_iff

@[to_additive]
protected theorem MulAction.bijective (g : Œ±) : Function.Bijective ((¬∑ ‚Ä¢ ¬∑) g : Œ≤ ‚Üí Œ≤) :=
  (MulAction.toPerm g).bijective
#align mul_action.bijective MulAction.bijective
#align add_action.bijective AddAction.bijective

@[to_additive]
protected theorem MulAction.injective (g : Œ±) : Function.Injective ((¬∑ ‚Ä¢ ¬∑) g : Œ≤ ‚Üí Œ≤) :=
  (MulAction.bijective g).injective
#align mul_action.injective MulAction.injective
#align add_action.injective AddAction.injective

@[to_additive]
protected theorem MulAction.surjective (g : Œ±) : Function.Surjective ((¬∑ ‚Ä¢ ¬∑) g : Œ≤ ‚Üí Œ≤) :=
  (MulAction.bijective g).surjective
#align mul_action.surjective MulAction.surjective
#align add_action.surjective AddAction.surjective

@[to_additive]
theorem smul_left_cancel (g : Œ±) {x y : Œ≤} (h : g ‚Ä¢ x = g ‚Ä¢ y) : x = y :=
  MulAction.injective g h
#align smul_left_cancel smul_left_cancel
#align vadd_left_cancel vadd_left_cancel

@[to_additive (attr := simp)]
theorem smul_left_cancel_iff (g : Œ±) {x y : Œ≤} : g ‚Ä¢ x = g ‚Ä¢ y ‚Üî x = y :=
  (MulAction.injective g).eq_iff
#align smul_left_cancel_iff smul_left_cancel_iff
#align vadd_left_cancel_iff vadd_left_cancel_iff

@[to_additive]
theorem smul_eq_iff_eq_inv_smul (g : Œ±) {x y : Œ≤} : g ‚Ä¢ x = y ‚Üî x = g‚Åª¬π ‚Ä¢ y :=
  (MulAction.toPerm g).apply_eq_iff_eq_symm_apply
#align smul_eq_iff_eq_inv_smul smul_eq_iff_eq_inv_smul
#align vadd_eq_iff_eq_neg_vadd vadd_eq_iff_eq_neg_vadd

end Group

/-- `Monoid.toMulAction` is faithful on nontrivial cancellative monoids with zero. -/
instance CancelMonoidWithZero.faithfulSMul [CancelMonoidWithZero Œ±] [Nontrivial Œ±] :
    FaithfulSMul Œ± Œ± :=
  ‚ü®fun h => mul_left_injective‚ÇÄ one_ne_zero (h 1)‚ü©
#align cancel_monoid_with_zero.to_has_faithful_smul CancelMonoidWithZero.faithfulSMul

section Gwz

variable [GroupWithZero Œ±] [MulAction Œ± Œ≤] {a : Œ±}

@[simp]
theorem inv_smul_smul‚ÇÄ {c : Œ±} (hc : c ‚â† 0) (x : Œ≤) : c‚Åª¬π ‚Ä¢ c ‚Ä¢ x = x :=
  inv_smul_smul (Units.mk0 c hc) x
#align inv_smul_smul‚ÇÄ inv_smul_smul‚ÇÄ

@[simp]
theorem smul_inv_smul‚ÇÄ {c : Œ±} (hc : c ‚â† 0) (x : Œ≤) : c ‚Ä¢ c‚Åª¬π ‚Ä¢ x = x :=
  smul_inv_smul (Units.mk0 c hc) x
#align smul_inv_smul‚ÇÄ smul_inv_smul‚ÇÄ

theorem inv_smul_eq_iff‚ÇÄ {a : Œ±} (ha : a ‚â† 0) {x y : Œ≤} : a‚Åª¬π ‚Ä¢ x = y ‚Üî x = a ‚Ä¢ y :=
  ‚ü®fun h => by rw [‚Üê h, smul_inv_smul‚ÇÄ ha], fun h => by rw [h, inv_smul_smul‚ÇÄ ha]‚ü©
               -- üéâ no goals
                                                        -- üéâ no goals
#align inv_smul_eq_iff‚ÇÄ inv_smul_eq_iff‚ÇÄ

theorem eq_inv_smul_iff‚ÇÄ {a : Œ±} (ha : a ‚â† 0) {x y : Œ≤} : x = a‚Åª¬π ‚Ä¢ y ‚Üî a ‚Ä¢ x = y :=
  (MulAction.toPerm (Units.mk0 a ha)).eq_symm_apply
#align eq_inv_smul_iff‚ÇÄ eq_inv_smul_iff‚ÇÄ

@[simp]
theorem Commute.smul_right_iff‚ÇÄ [Mul Œ≤] [SMulCommClass Œ± Œ≤ Œ≤] [IsScalarTower Œ± Œ≤ Œ≤] {a b : Œ≤}
    {c : Œ±} (hc : c ‚â† 0) : Commute a (c ‚Ä¢ b) ‚Üî Commute a b :=
  Commute.smul_right_iff (Units.mk0 c hc)
#align commute.smul_right_iff‚ÇÄ Commute.smul_right_iff‚ÇÄ

@[simp]
theorem Commute.smul_left_iff‚ÇÄ [Mul Œ≤] [SMulCommClass Œ± Œ≤ Œ≤] [IsScalarTower Œ± Œ≤ Œ≤] {a b : Œ≤} {c : Œ±}
    (hc : c ‚â† 0) : Commute (c ‚Ä¢ a) b ‚Üî Commute a b :=
  Commute.smul_left_iff (Units.mk0 c hc)
#align commute.smul_left_iff‚ÇÄ Commute.smul_left_iff‚ÇÄ

protected theorem MulAction.bijective‚ÇÄ (ha : a ‚â† 0) : Function.Bijective ((¬∑ ‚Ä¢ ¬∑) a : Œ≤ ‚Üí Œ≤) :=
  MulAction.bijective <| Units.mk0 a ha
#align mul_action.bijective‚ÇÄ MulAction.bijective‚ÇÄ

protected theorem MulAction.injective‚ÇÄ (ha : a ‚â† 0) : Function.Injective ((¬∑ ‚Ä¢ ¬∑) a : Œ≤ ‚Üí Œ≤) :=
  (MulAction.bijective‚ÇÄ ha).injective
#align mul_action.injective‚ÇÄ MulAction.injective‚ÇÄ

protected theorem MulAction.surjective‚ÇÄ (ha : a ‚â† 0) : Function.Surjective ((¬∑ ‚Ä¢ ¬∑) a : Œ≤ ‚Üí Œ≤) :=
  (MulAction.bijective‚ÇÄ ha).surjective
#align mul_action.surjective‚ÇÄ MulAction.surjective‚ÇÄ

end Gwz

end MulAction

section DistribMulAction

section Group

variable [Group Œ±] [AddMonoid Œ≤] [DistribMulAction Œ± Œ≤]

variable (Œ≤)

/-- Each element of the group defines an additive monoid isomorphism.

This is a stronger version of `MulAction.toPerm`. -/
@[simps (config := { simpRhs := true })]
def DistribMulAction.toAddEquiv (x : Œ±) : Œ≤ ‚âÉ+ Œ≤ :=
  { DistribMulAction.toAddMonoidHom Œ≤ x, MulAction.toPermHom Œ± Œ≤ x with }
#align distrib_mul_action.to_add_equiv DistribMulAction.toAddEquiv
#align distrib_mul_action.to_add_equiv_apply DistribMulAction.toAddEquiv_apply
#align distrib_mul_action.to_add_equiv_symm_apply DistribMulAction.toAddEquiv_symm_apply

variable (Œ±)

/-- Each element of the group defines an additive monoid isomorphism.

This is a stronger version of `MulAction.toPermHom`. -/
@[simps]
def DistribMulAction.toAddAut : Œ± ‚Üí* AddAut Œ≤ where
  toFun := DistribMulAction.toAddEquiv Œ≤
  map_one' := AddEquiv.ext (one_smul _)
  map_mul' _ _ := AddEquiv.ext (mul_smul _ _)
#align distrib_mul_action.to_add_aut DistribMulAction.toAddAut
#align distrib_mul_action.to_add_aut_apply DistribMulAction.toAddAut_apply

variable {Œ± Œ≤}

theorem smul_eq_zero_iff_eq (a : Œ±) {x : Œ≤} : a ‚Ä¢ x = 0 ‚Üî x = 0 :=
  ‚ü®fun h => by rw [‚Üê inv_smul_smul a x, h, smul_zero], fun h => h.symm ‚ñ∏ smul_zero _‚ü©
               -- üéâ no goals
#align smul_eq_zero_iff_eq smul_eq_zero_iff_eq

theorem smul_ne_zero_iff_ne (a : Œ±) {x : Œ≤} : a ‚Ä¢ x ‚â† 0 ‚Üî x ‚â† 0 :=
  not_congr <| smul_eq_zero_iff_eq a
#align smul_ne_zero_iff_ne smul_ne_zero_iff_ne

end Group

section Gwz

variable [GroupWithZero Œ±] [AddMonoid Œ≤] [DistribMulAction Œ± Œ≤]

theorem smul_eq_zero_iff_eq' {a : Œ±} (ha : a ‚â† 0) {x : Œ≤} : a ‚Ä¢ x = 0 ‚Üî x = 0 :=
  show Units.mk0 a ha ‚Ä¢ x = 0 ‚Üî x = 0 from smul_eq_zero_iff_eq _
#align smul_eq_zero_iff_eq' smul_eq_zero_iff_eq'

theorem smul_ne_zero_iff_ne' {a : Œ±} (ha : a ‚â† 0) {x : Œ≤} : a ‚Ä¢ x ‚â† 0 ‚Üî x ‚â† 0 :=
  show Units.mk0 a ha ‚Ä¢ x ‚â† 0 ‚Üî x ‚â† 0 from smul_ne_zero_iff_ne _
#align smul_ne_zero_iff_ne' smul_ne_zero_iff_ne'

end Gwz

end DistribMulAction

section MulDistribMulAction

variable [Group Œ±] [Monoid Œ≤] [MulDistribMulAction Œ± Œ≤]

variable (Œ≤)

/-- Each element of the group defines a multiplicative monoid isomorphism.

This is a stronger version of `MulAction.toPerm`. -/
@[simps (config := { simpRhs := true })]
def MulDistribMulAction.toMulEquiv (x : Œ±) : Œ≤ ‚âÉ* Œ≤ :=
  { MulDistribMulAction.toMonoidHom Œ≤ x, MulAction.toPermHom Œ± Œ≤ x with }
#align mul_distrib_mul_action.to_mul_equiv MulDistribMulAction.toMulEquiv
#align mul_distrib_mul_action.to_mul_equiv_symm_apply MulDistribMulAction.toMulEquiv_symm_apply
#align mul_distrib_mul_action.to_mul_equiv_apply MulDistribMulAction.toMulEquiv_apply

variable (Œ±)

/-- Each element of the group defines a multiplicative monoid isomorphism.

This is a stronger version of `MulAction.toPermHom`. -/
@[simps]
def MulDistribMulAction.toMulAut : Œ± ‚Üí* MulAut Œ≤ where
  toFun := MulDistribMulAction.toMulEquiv Œ≤
  map_one' := MulEquiv.ext (one_smul _)
  map_mul' _ _ := MulEquiv.ext (mul_smul _ _)
#align mul_distrib_mul_action.to_mul_aut MulDistribMulAction.toMulAut
#align mul_distrib_mul_action.to_mul_aut_apply MulDistribMulAction.toMulAut_apply

variable {Œ± Œ≤}

end MulDistribMulAction

section Arrow

/-- If `G` acts on `A`, then it acts also on `A ‚Üí B`, by `(g ‚Ä¢ F) a = F (g‚Åª¬π ‚Ä¢ a)`. -/
@[to_additive (attr := simps) arrowAddAction
      "If `G` acts on `A`, then it acts also on `A ‚Üí B`, by `(g +·µ• F) a = F (g‚Åª¬π +·µ• a)`"]
def arrowAction {G A B : Type*} [DivisionMonoid G] [MulAction G A] : MulAction G (A ‚Üí B) where
  smul g F a := F (g‚Åª¬π ‚Ä¢ a)
  one_smul := by
    intro f
    -- ‚ä¢ 1 ‚Ä¢ f = f
    show (fun x => f ((1 : G)‚Åª¬π ‚Ä¢ x)) = f
    -- ‚ä¢ (fun x => f (1‚Åª¬π ‚Ä¢ x)) = f
    simp only [inv_one, one_smul]
    -- üéâ no goals
  mul_smul := by
    intros x y f
    -- ‚ä¢ (x * y) ‚Ä¢ f = x ‚Ä¢ y ‚Ä¢ f
    show (fun a => f ((x*y)‚Åª¬π ‚Ä¢ a)) = (fun a => f (y‚Åª¬π ‚Ä¢ x‚Åª¬π ‚Ä¢ a))
    -- ‚ä¢ (fun a => f ((x * y)‚Åª¬π ‚Ä¢ a)) = fun a => f (y‚Åª¬π ‚Ä¢ x‚Åª¬π ‚Ä¢ a)
    simp only [mul_smul, mul_inv_rev]
    -- üéâ no goals
#align arrow_action arrowAction
#align arrow_add_action arrowAddAction

attribute [local instance] arrowAction

/-- When `B` is a monoid, `ArrowAction` is additionally a `MulDistribMulAction`. -/
def arrowMulDistribMulAction {G A B : Type*} [Group G] [MulAction G A] [Monoid B] :
    MulDistribMulAction G (A ‚Üí B) where
  smul_one _ := rfl
  smul_mul _ _ _ := rfl
#align arrow_mul_distrib_mul_action arrowMulDistribMulAction

attribute [local instance] arrowMulDistribMulAction

/-- Given groups `G H` with `G` acting on `A`, `G` acts by
  multiplicative automorphisms on `A ‚Üí H`. -/
@[simps!]
def mulAutArrow {G A H} [Group G] [MulAction G A] [Monoid H] : G ‚Üí* MulAut (A ‚Üí H) :=
  MulDistribMulAction.toMulAut _ _
#align mul_aut_arrow mulAutArrow
#align mul_aut_arrow_apply_apply mulAutArrow_apply_apply
#align mul_aut_arrow_apply_symm_apply mulAutArrow_apply_symm_apply

end Arrow

namespace IsUnit

section MulAction

variable [Monoid Œ±] [MulAction Œ± Œ≤]

@[to_additive]
theorem smul_left_cancel {a : Œ±} (ha : IsUnit a) {x y : Œ≤} : a ‚Ä¢ x = a ‚Ä¢ y ‚Üî x = y :=
  let ‚ü®u, hu‚ü© := ha
  hu ‚ñ∏ smul_left_cancel_iff u
#align is_unit.smul_left_cancel IsUnit.smul_left_cancel
#align is_add_unit.vadd_left_cancel IsAddUnit.vadd_left_cancel

end MulAction

section DistribMulAction

variable [Monoid Œ±] [AddMonoid Œ≤] [DistribMulAction Œ± Œ≤]

@[simp]
theorem smul_eq_zero {u : Œ±} (hu : IsUnit u) {x : Œ≤} : u ‚Ä¢ x = 0 ‚Üî x = 0 :=
  (Exists.elim hu) fun u hu => hu ‚ñ∏ show u ‚Ä¢ x = 0 ‚Üî x = 0 from smul_eq_zero_iff_eq u
#align is_unit.smul_eq_zero IsUnit.smul_eq_zero

end DistribMulAction

end IsUnit

section SMul

variable [Group Œ±] [Monoid Œ≤]

@[simp]
theorem isUnit_smul_iff [MulAction Œ± Œ≤] [SMulCommClass Œ± Œ≤ Œ≤] [IsScalarTower Œ± Œ≤ Œ≤] (g : Œ±)
    (m : Œ≤) : IsUnit (g ‚Ä¢ m) ‚Üî IsUnit m :=
  ‚ü®fun h => inv_smul_smul g m ‚ñ∏ h.smul g‚Åª¬π, IsUnit.smul g‚ü©
#align is_unit_smul_iff isUnit_smul_iff

theorem IsUnit.smul_sub_iff_sub_inv_smul [AddGroup Œ≤] [DistribMulAction Œ± Œ≤] [IsScalarTower Œ± Œ≤ Œ≤]
    [SMulCommClass Œ± Œ≤ Œ≤] (r : Œ±) (a : Œ≤) : IsUnit (r ‚Ä¢ (1 : Œ≤) - a) ‚Üî IsUnit (1 - r‚Åª¬π ‚Ä¢ a) := by
  rw [‚Üê isUnit_smul_iff r (1 - r‚Åª¬π ‚Ä¢ a), smul_sub, smul_inv_smul]
  -- üéâ no goals
#align is_unit.smul_sub_iff_sub_inv_smul IsUnit.smul_sub_iff_sub_inv_smul

end SMul
