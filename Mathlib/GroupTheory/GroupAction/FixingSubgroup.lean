/-
Copyright (c) 2022 Antoine Chambert-Loir. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Antoine Chambert-Loir
-/
import Mathlib.GroupTheory.Subgroup.Actions
import Mathlib.GroupTheory.GroupAction.Basic

#align_import group_theory.group_action.fixing_subgroup from "leanprover-community/mathlib"@"f93c11933efbc3c2f0299e47b8ff83e9b539cbf6"

/-!

# Fixing submonoid, fixing subgroup of an action

In the presence of an action of a monoid or a group,
this file defines the fixing submonoid or the fixing subgroup,
and relates it to the set of fixed points via a Galois connection.

## Main definitions

* `fixingSubmonoid M s` : in the presence of `MulAction M Î±` (with `Monoid M`)
  it is the `Submonoid M` consisting of elements which fix `s : Set Î±` pointwise.

* `fixingSubmonoid_fixedPoints_gc M Î±` is the `GaloisConnection`
  that relates `fixingSubmonoid` with `fixedPoints`.

* `fixingSubgroup M s` : in the presence of `MulAction M Î±` (with `Group M`)
  it is the `Subgroup M` consisting of elements which fix `s : Set Î±` pointwise.

* `fixingSubgroup_fixedPoints_gc M Î±` is the `GaloisConnection`
  that relates `fixingSubgroup` with `fixedPoints`.

TODO :

* Maybe other lemmas are useful

* Treat semigroups ?

-/


section Monoid

open MulAction

variable (M : Type*) {Î± : Type*} [Monoid M] [MulAction M Î±]

/-- The submonoid fixing a set under a `MulAction`. -/
@[to_additive " The additive submonoid fixing a set under an `AddAction`. "]
def fixingSubmonoid (s : Set Î±) : Submonoid M
    where
  carrier := { Ï• : M | âˆ€ x : s, Ï• â€¢ (x : Î±) = x }
  one_mem' _ := one_smul _ _
  mul_mem' {x y} hx hy z := by rw [mul_smul, hy z, hx z]
                               -- ðŸŽ‰ no goals
#align fixing_submonoid fixingSubmonoid
#align fixing_add_submonoid fixingAddSubmonoid

theorem mem_fixingSubmonoid_iff {s : Set Î±} {m : M} :
    m âˆˆ fixingSubmonoid M s â†” âˆ€ y âˆˆ s, m â€¢ y = y :=
  âŸ¨fun hg y hy => hg âŸ¨y, hyâŸ©, fun h âŸ¨y, hyâŸ© => h y hyâŸ©
#align mem_fixing_submonoid_iff mem_fixingSubmonoid_iff

variable (Î±)

/-- The Galois connection between fixing submonoids and fixed points of a monoid action -/
theorem fixingSubmonoid_fixedPoints_gc :
    GaloisConnection (OrderDual.toDual âˆ˜ fixingSubmonoid M)
      ((fun P : Submonoid M => fixedPoints P Î±) âˆ˜ OrderDual.ofDual) :=
  fun _s _P => âŸ¨fun h s hs p => h p.2 âŸ¨s, hsâŸ©, fun h p hp s => h s.2 âŸ¨p, hpâŸ©âŸ©
#align fixing_submonoid_fixed_points_gc fixingSubmonoid_fixedPoints_gc

theorem fixingSubmonoid_antitone : Antitone fun s : Set Î± => fixingSubmonoid M s :=
  (fixingSubmonoid_fixedPoints_gc M Î±).monotone_l
#align fixing_submonoid_antitone fixingSubmonoid_antitone

theorem fixedPoints_antitone : Antitone fun P : Submonoid M => fixedPoints P Î± :=
  (fixingSubmonoid_fixedPoints_gc M Î±).monotone_u.dual_left
#align fixed_points_antitone fixedPoints_antitone

/-- Fixing submonoid of union is intersection -/
theorem fixingSubmonoid_union {s t : Set Î±} :
    fixingSubmonoid M (s âˆª t) = fixingSubmonoid M s âŠ“ fixingSubmonoid M t :=
  (fixingSubmonoid_fixedPoints_gc M Î±).l_sup
#align fixing_submonoid_union fixingSubmonoid_union

/-- Fixing submonoid of iUnion is intersection -/
theorem fixingSubmonoid_iUnion {Î¹ : Sort*} {s : Î¹ â†’ Set Î±} :
    fixingSubmonoid M (â‹ƒ i, s i) = â¨… i, fixingSubmonoid M (s i) :=
  (fixingSubmonoid_fixedPoints_gc M Î±).l_iSup
#align fixing_submonoid_Union fixingSubmonoid_iUnion

/-- Fixed points of sup of submonoids is intersection -/
theorem fixedPoints_submonoid_sup {P Q : Submonoid M} :
    fixedPoints (â†¥(P âŠ” Q)) Î± = fixedPoints P Î± âˆ© fixedPoints Q Î± :=
  (fixingSubmonoid_fixedPoints_gc M Î±).u_inf
#align fixed_points_submonoid_sup fixedPoints_submonoid_sup

/-- Fixed points of iSup of submonoids is intersection -/
theorem fixedPoints_submonoid_iSup {Î¹ : Sort*} {P : Î¹ â†’ Submonoid M} :
    fixedPoints (â†¥(iSup P)) Î± = â‹‚ i, fixedPoints (P i) Î± :=
  (fixingSubmonoid_fixedPoints_gc M Î±).u_iInf
#align fixed_points_submonoid_supr fixedPoints_submonoid_iSup

end Monoid

section Group

open MulAction

variable (M : Type*) {Î± : Type*} [Group M] [MulAction M Î±]

/-- The subgroup fixing a set under a `MulAction`. -/
@[to_additive " The additive subgroup fixing a set under an `AddAction`. "]
def fixingSubgroup (s : Set Î±) : Subgroup M :=
  { fixingSubmonoid M s with inv_mem' := fun hx z => by rw [inv_smul_eq_iff, hx z] }
                                                        -- ðŸŽ‰ no goals
#align fixing_subgroup fixingSubgroup
#align fixing_add_subgroup fixingAddSubgroup

theorem mem_fixingSubgroup_iff {s : Set Î±} {m : M} : m âˆˆ fixingSubgroup M s â†” âˆ€ y âˆˆ s, m â€¢ y = y :=
  âŸ¨fun hg y hy => hg âŸ¨y, hyâŸ©, fun h âŸ¨y, hyâŸ© => h y hyâŸ©
#align mem_fixing_subgroup_iff mem_fixingSubgroup_iff

variable (Î±)

/-- The Galois connection between fixing subgroups and fixed points of a group action -/
theorem fixingSubgroup_fixedPoints_gc :
    GaloisConnection (OrderDual.toDual âˆ˜ fixingSubgroup M)
      ((fun P : Subgroup M => fixedPoints P Î±) âˆ˜ OrderDual.ofDual) :=
  fun _s _P => âŸ¨fun h s hs p => h p.2 âŸ¨s, hsâŸ©, fun h p hp s => h s.2 âŸ¨p, hpâŸ©âŸ©
#align fixing_subgroup_fixed_points_gc fixingSubgroup_fixedPoints_gc

theorem fixingSubgroup_antitone : Antitone (fixingSubgroup M : Set Î± â†’ Subgroup M) :=
  (fixingSubgroup_fixedPoints_gc M Î±).monotone_l
#align fixing_subgroup_antitone fixingSubgroup_antitone

theorem fixedPoints_subgroup_antitone : Antitone fun P : Subgroup M => fixedPoints P Î± :=
  (fixingSubgroup_fixedPoints_gc M Î±).monotone_u.dual_left
#align fixed_points_subgroup_antitone fixedPoints_subgroup_antitone

/-- Fixing subgroup of union is intersection -/
theorem fixingSubgroup_union {s t : Set Î±} :
    fixingSubgroup M (s âˆª t) = fixingSubgroup M s âŠ“ fixingSubgroup M t :=
  (fixingSubgroup_fixedPoints_gc M Î±).l_sup
#align fixing_subgroup_union fixingSubgroup_union

/-- Fixing subgroup of iUnion is intersection -/
theorem fixingSubgroup_iUnion {Î¹ : Sort*} {s : Î¹ â†’ Set Î±} :
    fixingSubgroup M (â‹ƒ i, s i) = â¨… i, fixingSubgroup M (s i) :=
  (fixingSubgroup_fixedPoints_gc M Î±).l_iSup
#align fixing_subgroup_Union fixingSubgroup_iUnion

/-- Fixed points of sup of subgroups is intersection -/
theorem fixedPoints_subgroup_sup {P Q : Subgroup M} :
    fixedPoints (â†¥(P âŠ” Q)) Î± = fixedPoints P Î± âˆ© fixedPoints Q Î± :=
  (fixingSubgroup_fixedPoints_gc M Î±).u_inf
#align fixed_points_subgroup_sup fixedPoints_subgroup_sup

/-- Fixed points of iSup of subgroups is intersection -/
theorem fixedPoints_subgroup_iSup {Î¹ : Sort*} {P : Î¹ â†’ Subgroup M} :
    fixedPoints (â†¥(iSup P)) Î± = â‹‚ i, fixedPoints (P i) Î± :=
  (fixingSubgroup_fixedPoints_gc M Î±).u_iInf
#align fixed_points_subgroup_supr fixedPoints_subgroup_iSup

end Group
