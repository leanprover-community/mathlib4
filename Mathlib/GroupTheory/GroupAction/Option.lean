/-
Copyright (c) 2022 Ya├Фl Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya├Фl Dillies
-/
import Mathlib.GroupTheory.GroupAction.Defs

#align_import group_theory.group_action.option from "leanprover-community/mathlib"@"f1a2caaf51ef593799107fe9a8d5e411599f3996"

/-!
# Option instances for additive and multiplicative actions

This file defines instances for additive and multiplicative actions on `Option` type. Scalar
multiplication is defined by `a Рђб some b = some (a Рђб b)` and `a Рђб none = none`.

## See also

* `GroupTheory.GroupAction.Pi`
* `GroupTheory.GroupAction.Prod`
* `GroupTheory.GroupAction.Sigma`
* `GroupTheory.GroupAction.Sum`
-/


variable {M N ╬▒ : Type*}

namespace Option

section SMul

variable [SMul M ╬▒] [SMul N ╬▒] (a : M) (b : ╬▒) (x : Option ╬▒)

@[to_additive Option.VAdd]
instance : SMul M (Option ╬▒) :=
  РЪеfun a => Option.map <| (a Рђб ┬и)РЪЕ

@[to_additive]
theorem smul_def : a Рђб x = x.map ((┬и Рђб ┬и) a) :=
  rfl
#align option.smul_def Option.smul_def
#align option.vadd_def Option.vadd_def

@[to_additive (attr := simp)]
theorem smul_none : a Рђб (none : Option ╬▒) = none :=
  rfl
#align option.smul_none Option.smul_none
#align option.vadd_none Option.vadd_none

@[to_additive (attr := simp)]
theorem smul_some : a Рђб some b = some (a Рђб b) :=
  rfl
#align option.smul_some Option.smul_some
#align option.vadd_some Option.vadd_some

@[to_additive]
instance [SMul M N] [IsScalarTower M N ╬▒] : IsScalarTower M N (Option ╬▒) :=
  РЪеfun a b x => by
    cases x
    -- Ріб (a Рђб b) Рђб none = a Рђб b Рђб none
    exacts [rfl, congr_arg some (smul_assoc _ _ _)]РЪЕ
    -- ­ЪјЅ no goals

@[to_additive]
instance [SMulCommClass M N ╬▒] : SMulCommClass M N (Option ╬▒) :=
  РЪеfun _ _ => Function.Commute.option_map <| smul_comm _ _РЪЕ

@[to_additive]
instance [SMul Mрхљрхњрхќ ╬▒] [IsCentralScalar M ╬▒] : IsCentralScalar M (Option ╬▒) :=
  РЪеfun a x => by
    cases x
    -- Ріб MulOpposite.op a Рђб none = a Рђб none
    exacts [rfl, congr_arg some (op_smul_eq_smul _ _)]РЪЕ
    -- ­ЪјЅ no goals

@[to_additive]
instance [FaithfulSMul M ╬▒] : FaithfulSMul M (Option ╬▒) :=
  РЪеfun h => eq_of_smul_eq_smul fun b : ╬▒ => by injection h (some b)РЪЕ
                                               -- ­ЪјЅ no goals

end SMul

instance [Monoid M] [MulAction M ╬▒] :
    MulAction M (Option ╬▒) where
  smul := (┬и Рђб ┬и)
  one_smul b := by
    cases b
    -- Ріб 1 Рђб none = none
    exacts [rfl, congr_arg some (one_smul _ _)]
    -- ­ЪјЅ no goals
  mul_smul aРѓЂ aРѓѓ b := by
    cases b
    -- Ріб (aРѓЂ * aРѓѓ) Рђб none = aРѓЂ Рђб aРѓѓ Рђб none
    exacts [rfl, congr_arg some (mul_smul _ _ _)]
    -- ­ЪјЅ no goals

end Option
