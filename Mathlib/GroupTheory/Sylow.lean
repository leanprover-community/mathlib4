/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Thomas Browning
-/
import Mathlib.Data.Nat.Factorization.Basic
import Mathlib.Data.SetLike.Fintype
import Mathlib.GroupTheory.GroupAction.ConjAct
import Mathlib.GroupTheory.PGroup
import Mathlib.GroupTheory.NoncommPiCoprod
import Mathlib.Order.Atoms.Finite

#align_import group_theory.sylow from "leanprover-community/mathlib"@"4be589053caf347b899a494da75410deb55fb3ef"

/-!
# Sylow theorems

The Sylow theorems are the following results for every finite group `G` and every prime number `p`.

* There exists a Sylow `p`-subgroup of `G`.
* All Sylow `p`-subgroups of `G` are conjugate to each other.
* Let `nâ‚š` be the number of Sylow `p`-subgroups of `G`, then `nâ‚š` divides the index of the Sylow
  `p`-subgroup, `nâ‚š â‰¡ 1 [MOD p]`, and `nâ‚š` is equal to the index of the normalizer of the Sylow
  `p`-subgroup in `G`.

## Main definitions

* `Sylow p G` : The type of Sylow `p`-subgroups of `G`.

## Main statements

* `exists_subgroup_card_pow_prime`: A generalization of Sylow's first theorem:
  For every prime power `pâ¿` dividing the cardinality of `G`,
  there exists a subgroup of `G` of order `pâ¿`.
* `IsPGroup.exists_le_sylow`: A generalization of Sylow's first theorem:
  Every `p`-subgroup is contained in a Sylow `p`-subgroup.
* `Sylow.card_eq_multiplicity`: The cardinality of a Sylow subgroup is `p ^ n`
 where `n` is the multiplicity of `p` in the group order.
* `sylow_conjugate`: A generalization of Sylow's second theorem:
  If the number of Sylow `p`-subgroups is finite, then all Sylow `p`-subgroups are conjugate.
* `card_sylow_modEq_one`: A generalization of Sylow's third theorem:
  If the number of Sylow `p`-subgroups is finite, then it is congruent to `1` modulo `p`.
-/


open Fintype MulAction Subgroup

section InfiniteSylow

variable (p : â„•) (G : Type*) [Group G]

/-- A Sylow `p`-subgroup is a maximal `p`-subgroup. -/
structure Sylow extends Subgroup G where
  isPGroup' : IsPGroup p toSubgroup
  is_maximal' : âˆ€ {Q : Subgroup G}, IsPGroup p Q â†’ toSubgroup â‰¤ Q â†’ Q = toSubgroup
#align sylow Sylow

variable {p} {G}

namespace Sylow

attribute [coe] Sylow.toSubgroup

--Porting note: Changed to `CoeOut`
instance : CoeOut (Sylow p G) (Subgroup G) :=
  âŸ¨Sylow.toSubgroupâŸ©

-- Porting note: syntactic tautology
-- @[simp]
-- theorem toSubgroup_eq_coe {P : Sylow p G} : P.toSubgroup = â†‘P :=
--   rfl
#noalign sylow.to_subgroup_eq_coe

@[ext]
theorem ext {P Q : Sylow p G} (h : (P : Subgroup G) = Q) : P = Q := by cases P; cases Q; congr
                                                                       -- âŠ¢ { toSubgroup := toSubgroupâœ, isPGroup' := isPGroup'âœ, is_maximal' := is_maxi â€¦
                                                                                -- âŠ¢ { toSubgroup := toSubgroupâœÂ¹, isPGroup' := isPGroup'âœÂ¹, is_maximal' := is_ma â€¦
                                                                                         -- ğŸ‰ no goals
#align sylow.ext Sylow.ext

theorem ext_iff {P Q : Sylow p G} : P = Q â†” (P : Subgroup G) = Q :=
  âŸ¨congr_arg _, extâŸ©
#align sylow.ext_iff Sylow.ext_iff

instance : SetLike (Sylow p G) G where
  coe := (â†‘)
  coe_injective' _ _ h := ext (SetLike.coe_injective h)

instance : SubgroupClass (Sylow p G) G where
  mul_mem := Subgroup.mul_mem _
  one_mem _ := Subgroup.one_mem _
  inv_mem := Subgroup.inv_mem _

variable (P : Sylow p G)

/-- The action by a Sylow subgroup is the action by the underlying group. -/
instance mulActionLeft {Î± : Type*} [MulAction G Î±] : MulAction P Î± :=
  inferInstanceAs (MulAction (P : Subgroup G) Î±)
#align sylow.mul_action_left Sylow.mulActionLeft

variable {K : Type*} [Group K] (Ï• : K â†’* G) {N : Subgroup G}

/-- The preimage of a Sylow subgroup under a p-group-kernel homomorphism is a Sylow subgroup. -/
def comapOfKerIsPGroup (hÏ• : IsPGroup p Ï•.ker) (h : â†‘P â‰¤ Ï•.range) : Sylow p K :=
  { P.1.comap Ï• with
    isPGroup' := P.2.comap_of_ker_isPGroup Ï• hÏ•
    is_maximal' := fun {Q} hQ hle => by
      show Q = P.1.comap Ï•
      -- âŠ¢ Q = comap Ï• â†‘P
      rw [â† P.3 (hQ.map Ï•) (le_trans (ge_of_eq (map_comap_eq_self h)) (map_mono hle))]
      -- âŠ¢ Q = comap Ï• (map Ï• Q)
      exact (comap_map_eq_self ((P.1.ker_le_comap Ï•).trans hle)).symm }
      -- ğŸ‰ no goals
#align sylow.comap_of_ker_is_p_group Sylow.comapOfKerIsPGroup

@[simp]
theorem coe_comapOfKerIsPGroup (hÏ• : IsPGroup p Ï•.ker) (h : â†‘P â‰¤ Ï•.range) :
    (P.comapOfKerIsPGroup Ï• hÏ• h : Subgroup K) = Subgroup.comap Ï• â†‘P :=
  rfl
#align sylow.coe_comap_of_ker_is_p_group Sylow.coe_comapOfKerIsPGroup

/-- The preimage of a Sylow subgroup under an injective homomorphism is a Sylow subgroup. -/
def comapOfInjective (hÏ• : Function.Injective Ï•) (h : â†‘P â‰¤ Ï•.range) : Sylow p K :=
  P.comapOfKerIsPGroup Ï• (IsPGroup.ker_isPGroup_of_injective hÏ•) h
#align sylow.comap_of_injective Sylow.comapOfInjective

@[simp]
theorem coe_comapOfInjective (hÏ• : Function.Injective Ï•) (h : â†‘P â‰¤ Ï•.range) :
    â†‘(P.comapOfInjective Ï• hÏ• h) = Subgroup.comap Ï• â†‘P :=
  rfl
#align sylow.coe_comap_of_injective Sylow.coe_comapOfInjective

/-- A sylow subgroup of G is also a sylow subgroup of a subgroup of G. -/
protected def subtype (h : â†‘P â‰¤ N) : Sylow p N :=
  P.comapOfInjective N.subtype Subtype.coe_injective (by rwa [subtype_range])
                                                         -- ğŸ‰ no goals
#align sylow.subtype Sylow.subtype

@[simp]
theorem coe_subtype (h : â†‘P â‰¤ N) : â†‘(P.subtype h) = subgroupOf (â†‘P) N :=
  rfl
#align sylow.coe_subtype Sylow.coe_subtype

theorem subtype_injective {P Q : Sylow p G} {hP : â†‘P â‰¤ N} {hQ : â†‘Q â‰¤ N}
    (h : P.subtype hP = Q.subtype hQ) : P = Q := by
  rw [SetLike.ext_iff] at h âŠ¢
  -- âŠ¢ âˆ€ (x : G), x âˆˆ P â†” x âˆˆ Q
  exact fun g => âŸ¨fun hg => (h âŸ¨g, hP hgâŸ©).mp hg, fun hg => (h âŸ¨g, hQ hgâŸ©).mpr hgâŸ©
  -- ğŸ‰ no goals
#align sylow.subtype_injective Sylow.subtype_injective

end Sylow

/-- A generalization of **Sylow's first theorem**.
  Every `p`-subgroup is contained in a Sylow `p`-subgroup. -/
theorem IsPGroup.exists_le_sylow {P : Subgroup G} (hP : IsPGroup p P) : âˆƒ Q : Sylow p G, P â‰¤ Q :=
  Exists.elim
    (zorn_nonempty_partialOrderâ‚€ { Q : Subgroup G | IsPGroup p Q }
      (fun c hc1 hc2 Q hQ =>
        âŸ¨{  carrier := â‹ƒ R : c, R
            one_mem' := âŸ¨Q, âŸ¨âŸ¨Q, hQâŸ©, rflâŸ©, Q.one_memâŸ©
            inv_mem' := fun {g} âŸ¨_, âŸ¨R, rflâŸ©, hgâŸ© => âŸ¨R, âŸ¨R, rflâŸ©, R.1.inv_mem hgâŸ©
            mul_mem' := fun {g} h âŸ¨_, âŸ¨R, rflâŸ©, hgâŸ© âŸ¨_, âŸ¨S, rflâŸ©, hhâŸ© =>
              (hc2.total R.2 S.2).elim (fun T => âŸ¨S, âŸ¨S, rflâŸ©, S.1.mul_mem (T hg) hhâŸ©) fun T =>
                âŸ¨R, âŸ¨R, rflâŸ©, R.1.mul_mem hg (T hh)âŸ© },
          fun âŸ¨g, _, âŸ¨S, rflâŸ©, hgâŸ© => by
          refine' Exists.imp (fun k hk => _) (hc1 S.2 âŸ¨g, hgâŸ©)
          -- âŠ¢ { val := g, property := (_ : âˆƒ t, (t âˆˆ Set.range fun R => â†‘â†‘R) âˆ§ g âˆˆ t) } ^  â€¦
          rwa [Subtype.ext_iff, coe_pow] at hk âŠ¢, fun M hM g hg => âŸ¨M, âŸ¨âŸ¨M, hMâŸ©, rflâŸ©, hgâŸ©âŸ©)
          -- ğŸ‰ no goals
      P hP)
    fun {Q} âŸ¨hQ1, hQ2, hQ3âŸ© => âŸ¨âŸ¨Q, hQ1, hQ3 _âŸ©, hQ2âŸ©
#align is_p_group.exists_le_sylow IsPGroup.exists_le_sylow

instance Sylow.nonempty : Nonempty (Sylow p G) :=
  nonempty_of_exists IsPGroup.of_bot.exists_le_sylow
#align sylow.nonempty Sylow.nonempty

noncomputable instance Sylow.inhabited : Inhabited (Sylow p G) :=
  Classical.inhabited_of_nonempty Sylow.nonempty
#align sylow.inhabited Sylow.inhabited

theorem Sylow.exists_comap_eq_of_ker_isPGroup {H : Type*} [Group H] (P : Sylow p H) {f : H â†’* G}
    (hf : IsPGroup p f.ker) : âˆƒ Q : Sylow p G, (Q : Subgroup G).comap f = P :=
  Exists.imp (fun Q hQ => P.3 (Q.2.comap_of_ker_isPGroup f hf) (map_le_iff_le_comap.mp hQ))
    (P.2.map f).exists_le_sylow
#align sylow.exists_comap_eq_of_ker_is_p_group Sylow.exists_comap_eq_of_ker_isPGroup

theorem Sylow.exists_comap_eq_of_injective {H : Type*} [Group H] (P : Sylow p H) {f : H â†’* G}
    (hf : Function.Injective f) : âˆƒ Q : Sylow p G, (Q : Subgroup G).comap f = P :=
  P.exists_comap_eq_of_ker_isPGroup (IsPGroup.ker_isPGroup_of_injective hf)
#align sylow.exists_comap_eq_of_injective Sylow.exists_comap_eq_of_injective

theorem Sylow.exists_comap_subtype_eq {H : Subgroup G} (P : Sylow p H) :
    âˆƒ Q : Sylow p G, (Q : Subgroup G).comap H.subtype = P :=
  P.exists_comap_eq_of_injective Subtype.coe_injective
#align sylow.exists_comap_subtype_eq Sylow.exists_comap_subtype_eq

/-- If the kernel of `f : H â†’* G` is a `p`-group,
  then `Fintype (Sylow p G)` implies `Fintype (Sylow p H)`. -/
noncomputable def Sylow.fintypeOfKerIsPGroup {H : Type*} [Group H] {f : H â†’* G}
    (hf : IsPGroup p f.ker) [Fintype (Sylow p G)] : Fintype (Sylow p H) :=
  let h_exists := fun P : Sylow p H => P.exists_comap_eq_of_ker_isPGroup hf
  let g : Sylow p H â†’ Sylow p G := fun P => Classical.choose (h_exists P)
  have hg : âˆ€ P : Sylow p H, (g P).1.comap f = P := fun P => Classical.choose_spec (h_exists P)
  Fintype.ofInjective g fun P Q h => Sylow.ext (by rw [â† hg, h]; exact (h_exists Q).choose_spec)
                                                   -- âŠ¢ comap f â†‘(g Q) = â†‘Q
                                                                 -- ğŸ‰ no goals
#align sylow.fintype_of_ker_is_p_group Sylow.fintypeOfKerIsPGroup

/-- If `f : H â†’* G` is injective, then `Fintype (Sylow p G)` implies `Fintype (Sylow p H)`. -/
noncomputable def Sylow.fintypeOfInjective {H : Type*} [Group H] {f : H â†’* G}
    (hf : Function.Injective f) [Fintype (Sylow p G)] : Fintype (Sylow p H) :=
  Sylow.fintypeOfKerIsPGroup (IsPGroup.ker_isPGroup_of_injective hf)
#align sylow.fintype_of_injective Sylow.fintypeOfInjective

/-- If `H` is a subgroup of `G`, then `Fintype (Sylow p G)` implies `Fintype (Sylow p H)`. -/
noncomputable instance (H : Subgroup G) [Fintype (Sylow p G)] : Fintype (Sylow p H) :=
  Sylow.fintypeOfInjective H.subtype_injective

/-- If `H` is a subgroup of `G`, then `Finite (Sylow p G)` implies `Finite (Sylow p H)`. -/
instance (H : Subgroup G) [Finite (Sylow p G)] : Finite (Sylow p H) := by
  cases nonempty_fintype (Sylow p G)
  -- âŠ¢ Finite (Sylow p { x // x âˆˆ H })
  infer_instance
  -- ğŸ‰ no goals

open Pointwise

/-- `Subgroup.pointwiseMulAction` preserves Sylow subgroups. -/
instance Sylow.pointwiseMulAction {Î± : Type*} [Group Î±] [MulDistribMulAction Î± G] :
    MulAction Î± (Sylow p G) where
  smul g P :=
    âŸ¨(g â€¢ P.toSubgroup : Subgroup G), P.2.map _, fun {Q} hQ hS =>
      inv_smul_eq_iff.mp
        (P.3 (hQ.map _) fun s hs =>
          (congr_arg (Â· âˆˆ gâ»Â¹ â€¢ Q) (inv_smul_smul g s)).mp
            (smul_mem_pointwise_smul (g â€¢ s) gâ»Â¹ Q (hS (smul_mem_pointwise_smul s g P hs))))âŸ©
  one_smul P := Sylow.ext (one_smul Î± P.toSubgroup)
  mul_smul g h P := Sylow.ext (mul_smul g h P.toSubgroup)
#align sylow.pointwise_mul_action Sylow.pointwiseMulAction

theorem Sylow.pointwise_smul_def {Î± : Type*} [Group Î±] [MulDistribMulAction Î± G] {g : Î±}
    {P : Sylow p G} : â†‘(g â€¢ P) = g â€¢ (P : Subgroup G) :=
  rfl
#align sylow.pointwise_smul_def Sylow.pointwise_smul_def

instance Sylow.mulAction : MulAction G (Sylow p G) :=
  compHom _ MulAut.conj
#align sylow.mul_action Sylow.mulAction

theorem Sylow.smul_def {g : G} {P : Sylow p G} : g â€¢ P = MulAut.conj g â€¢ P :=
  rfl
#align sylow.smul_def Sylow.smul_def

theorem Sylow.coe_subgroup_smul {g : G} {P : Sylow p G} :
    â†‘(g â€¢ P) = MulAut.conj g â€¢ (P : Subgroup G) :=
  rfl
#align sylow.coe_subgroup_smul Sylow.coe_subgroup_smul

theorem Sylow.coe_smul {g : G} {P : Sylow p G} : â†‘(g â€¢ P) = MulAut.conj g â€¢ (P : Set G) :=
  rfl
#align sylow.coe_smul Sylow.coe_smul

theorem Sylow.smul_le {P : Sylow p G} {H : Subgroup G} (hP : â†‘P â‰¤ H) (h : H) : â†‘(h â€¢ P) â‰¤ H :=
  Subgroup.conj_smul_le_of_le hP h
#align sylow.smul_le Sylow.smul_le

theorem Sylow.smul_subtype {P : Sylow p G} {H : Subgroup G} (hP : â†‘P â‰¤ H) (h : H) :
    h â€¢ P.subtype hP = (h â€¢ P).subtype (Sylow.smul_le hP h) :=
  Sylow.ext (Subgroup.conj_smul_subgroupOf hP h)
#align sylow.smul_subtype Sylow.smul_subtype

theorem Sylow.smul_eq_iff_mem_normalizer {g : G} {P : Sylow p G} :
    g â€¢ P = P â†” g âˆˆ (P : Subgroup G).normalizer := by
  rw [eq_comm, SetLike.ext_iff, â† inv_mem_iff (G := G) (H := normalizer P.toSubgroup),
      mem_normalizer_iff, inv_inv]
  exact
    forall_congr' fun h =>
      iff_congr Iff.rfl
        âŸ¨fun âŸ¨a, b, câŸ© => c â–¸ by simpa [mul_assoc] using b,
          fun hh => âŸ¨(MulAut.conj g)â»Â¹ h, hh, MulAut.apply_inv_self G (MulAut.conj g) hâŸ©âŸ©
#align sylow.smul_eq_iff_mem_normalizer Sylow.smul_eq_iff_mem_normalizer

theorem Sylow.smul_eq_of_normal {g : G} {P : Sylow p G} [h : (P : Subgroup G).Normal] : g â€¢ P = P :=
  by simp only [Sylow.smul_eq_iff_mem_normalizer, normalizer_eq_top.mpr h, mem_top]
     -- ğŸ‰ no goals
#align sylow.smul_eq_of_normal Sylow.smul_eq_of_normal

theorem Subgroup.sylow_mem_fixedPoints_iff (H : Subgroup G) {P : Sylow p G} :
    P âˆˆ fixedPoints H (Sylow p G) â†” H â‰¤ (P : Subgroup G).normalizer := by
  simp_rw [SetLike.le_def, â† Sylow.smul_eq_iff_mem_normalizer]; exact Subtype.forall
  -- âŠ¢ P âˆˆ fixedPoints { x // x âˆˆ H } (Sylow p G) â†” âˆ€ â¦ƒx : Gâ¦„, x âˆˆ H â†’ x â€¢ P = P
                                                                -- ğŸ‰ no goals
#align subgroup.sylow_mem_fixed_points_iff Subgroup.sylow_mem_fixedPoints_iff

theorem IsPGroup.inf_normalizer_sylow {P : Subgroup G} (hP : IsPGroup p P) (Q : Sylow p G) :
    P âŠ“ (Q : Subgroup G).normalizer = P âŠ“ Q :=
  le_antisymm
    (le_inf inf_le_left
      (sup_eq_right.mp
        (Q.3 (hP.to_inf_left.to_sup_of_normal_right' Q.2 inf_le_right) le_sup_right)))
    (inf_le_inf_left P le_normalizer)
#align is_p_group.inf_normalizer_sylow IsPGroup.inf_normalizer_sylow

theorem IsPGroup.sylow_mem_fixedPoints_iff {P : Subgroup G} (hP : IsPGroup p P) {Q : Sylow p G} :
    Q âˆˆ fixedPoints P (Sylow p G) â†” P â‰¤ Q := by
  rw [P.sylow_mem_fixedPoints_iff, â† inf_eq_left, hP.inf_normalizer_sylow, inf_eq_left]
  -- ğŸ‰ no goals
#align is_p_group.sylow_mem_fixed_points_iff IsPGroup.sylow_mem_fixedPoints_iff

/-- A generalization of **Sylow's second theorem**.
  If the number of Sylow `p`-subgroups is finite, then all Sylow `p`-subgroups are conjugate. -/
instance [hp : Fact p.Prime] [Finite (Sylow p G)] : IsPretransitive G (Sylow p G) :=
  âŸ¨fun P Q => by
    classical
      cases nonempty_fintype (Sylow p G)
      have H := fun {R : Sylow p G} {S : orbit G P} =>
        calc
          S âˆˆ fixedPoints R (orbit G P) â†” S.1 âˆˆ fixedPoints R (Sylow p G) :=
            forall_congr' fun a => Subtype.ext_iff
          _ â†” R.1 â‰¤ S := R.2.sylow_mem_fixedPoints_iff
          _ â†” S.1.1 = R := âŸ¨fun h => R.3 S.1.2 h, ge_of_eqâŸ©
      suffices Set.Nonempty (fixedPoints Q (orbit G P)) by
        exact Exists.elim this fun R hR => by
          rw [â† Sylow.ext (H.mp hR)]
          exact R.2
      apply Q.2.nonempty_fixed_point_of_prime_not_dvd_card
      refine' fun h => hp.out.not_dvd_one (Nat.modEq_zero_iff_dvd.mp _)
      calc
        1 = card (fixedPoints P (orbit G P)) := ?_
        _ â‰¡ card (orbit G P) [MOD p] := (P.2.card_modEq_card_fixedPoints (orbit G P)).symm
        _ â‰¡ 0 [MOD p] := Nat.modEq_zero_iff_dvd.mpr h
      rw [â† Set.card_singleton (âŸ¨P, mem_orbit_self PâŸ© : orbit G P)]
      refine' card_congr' (congr_arg _ (Eq.symm _))
      rw [Set.eq_singleton_iff_unique_mem]
      exact âŸ¨H.mpr rfl, fun R h => Subtype.ext (Sylow.ext (H.mp h))âŸ©âŸ©

variable (p) (G)

/-- A generalization of **Sylow's third theorem**.
  If the number of Sylow `p`-subgroups is finite, then it is congruent to `1` modulo `p`. -/
theorem card_sylow_modEq_one [Fact p.Prime] [Fintype (Sylow p G)] :
    card (Sylow p G) â‰¡ 1 [MOD p] := by
  refine' Sylow.nonempty.elim fun P : Sylow p G => _
  -- âŠ¢ card (Sylow p G) â‰¡ 1 [MOD p]
  have : fixedPoints P.1 (Sylow p G) = {P} :=
    Set.ext fun Q : Sylow p G =>
      calc
        Q âˆˆ fixedPoints P (Sylow p G) â†” P.1 â‰¤ Q := P.2.sylow_mem_fixedPoints_iff
        _ â†” Q.1 = P.1 := âŸ¨P.3 Q.2, ge_of_eqâŸ©
        _ â†” Q âˆˆ {P} := Sylow.ext_iff.symm.trans Set.mem_singleton_iff.symm

  have fin : Fintype (fixedPoints P.1 (Sylow p G)) := by
    rw [this]
    infer_instance
  have : card (fixedPoints P.1 (Sylow p G)) = 1 := by simp [this]
  -- âŠ¢ card (Sylow p G) â‰¡ 1 [MOD p]
  exact (P.2.card_modEq_card_fixedPoints (Sylow p G)).trans (by rw [this])
  -- ğŸ‰ no goals
#align card_sylow_modeq_one card_sylow_modEq_one

theorem not_dvd_card_sylow [hp : Fact p.Prime] [Fintype (Sylow p G)] : Â¬p âˆ£ card (Sylow p G) :=
  fun h =>
  hp.1.ne_one
    (Nat.dvd_one.mp
      ((Nat.modEq_iff_dvd' zero_le_one).mp
        ((Nat.modEq_zero_iff_dvd.mpr h).symm.trans (card_sylow_modEq_one p G))))
#align not_dvd_card_sylow not_dvd_card_sylow

variable {p} {G}

/-- Sylow subgroups are isomorphic -/
nonrec def Sylow.equivSMul (P : Sylow p G) (g : G) : P â‰ƒ* (g â€¢ P : Sylow p G) :=
  equivSMul (MulAut.conj g) P.toSubgroup
#align sylow.equiv_smul Sylow.equivSMul

/-- Sylow subgroups are isomorphic -/
noncomputable def Sylow.equiv [Fact p.Prime] [Finite (Sylow p G)] (P Q : Sylow p G) : P â‰ƒ* Q := by
  rw [â† Classical.choose_spec (exists_smul_eq G P Q)]
  -- âŠ¢ { x // x âˆˆ â†‘P } â‰ƒ* { x // x âˆˆ â†‘(Classical.choose (_ : âˆƒ m, m â€¢ P = Q) â€¢ P) }
  exact P.equivSMul (Classical.choose (exists_smul_eq G P Q))
  -- ğŸ‰ no goals
#align sylow.equiv Sylow.equiv

@[simp]
theorem Sylow.orbit_eq_top [Fact p.Prime] [Finite (Sylow p G)] (P : Sylow p G) : orbit G P = âŠ¤ :=
  top_le_iff.mp fun Q _ => exists_smul_eq G P Q
#align sylow.orbit_eq_top Sylow.orbit_eq_top

theorem Sylow.stabilizer_eq_normalizer (P : Sylow p G) :
    stabilizer G P = (P : Subgroup G).normalizer := by
  ext; simp [Sylow.smul_eq_iff_mem_normalizer]
  -- âŠ¢ xâœ âˆˆ stabilizer G P â†” xâœ âˆˆ normalizer â†‘P
       -- ğŸ‰ no goals
#align sylow.stabilizer_eq_normalizer Sylow.stabilizer_eq_normalizer

theorem Sylow.conj_eq_normalizer_conj_of_mem_centralizer [Fact p.Prime] [Finite (Sylow p G)]
    (P : Sylow p G) (x g : G) (hx : x âˆˆ centralizer (P : Set G))
    (hy : gâ»Â¹ * x * g âˆˆ centralizer (P : Set G)) :
    âˆƒ n âˆˆ (P : Subgroup G).normalizer, gâ»Â¹ * x * g = nâ»Â¹ * x * n := by
  have h1 : â†‘P â‰¤ centralizer (zpowers x : Set G) := by rwa [le_centralizer_iff, zpowers_le]
  -- âŠ¢ âˆƒ n, n âˆˆ normalizer â†‘P âˆ§ gâ»Â¹ * x * g = nâ»Â¹ * x * n
  have h2 : â†‘(g â€¢ P) â‰¤ centralizer (zpowers x : Set G) := by
    rw [le_centralizer_iff, zpowers_le]
    rintro - âŸ¨z, hz, rflâŸ©
    specialize hy z hz
    rwa [â† mul_assoc, â† eq_mul_inv_iff_mul_eq, mul_assoc, mul_assoc, mul_assoc, â† mul_assoc,
      eq_inv_mul_iff_mul_eq, â† mul_assoc, â† mul_assoc] at hy
  obtain âŸ¨h, hhâŸ© :=
    exists_smul_eq (centralizer (zpowers x : Set G)) ((g â€¢ P).subtype h2) (P.subtype h1)
  simp_rw [Sylow.smul_subtype, Subgroup.smul_def, smul_smul] at hh
  -- âŠ¢ âˆƒ n, n âˆˆ normalizer â†‘P âˆ§ gâ»Â¹ * x * g = nâ»Â¹ * x * n
  refine' âŸ¨h * g, Sylow.smul_eq_iff_mem_normalizer.mp (Sylow.subtype_injective hh), _âŸ©
  -- âŠ¢ gâ»Â¹ * x * g = (â†‘h * g)â»Â¹ * x * (â†‘h * g)
  rw [â† mul_assoc, Commute.right_comm (h.prop x (mem_zpowers x)), mul_inv_rev, inv_mul_cancel_right]
  -- ğŸ‰ no goals
#align sylow.conj_eq_normalizer_conj_of_mem_centralizer Sylow.conj_eq_normalizer_conj_of_mem_centralizer

theorem Sylow.conj_eq_normalizer_conj_of_mem [Fact p.Prime] [Finite (Sylow p G)] (P : Sylow p G)
    [_hP : (P : Subgroup G).IsCommutative] (x g : G) (hx : x âˆˆ P) (hy : gâ»Â¹ * x * g âˆˆ P) :
    âˆƒ n âˆˆ (P : Subgroup G).normalizer, gâ»Â¹ * x * g = nâ»Â¹ * x * n :=
  P.conj_eq_normalizer_conj_of_mem_centralizer x g (le_centralizer P hx) (le_centralizer P hy)
#align sylow.conj_eq_normalizer_conj_of_mem Sylow.conj_eq_normalizer_conj_of_mem

/-- Sylow `p`-subgroups are in bijection with cosets of the normalizer of a Sylow `p`-subgroup -/
noncomputable def Sylow.equivQuotientNormalizer [Fact p.Prime] [Fintype (Sylow p G)]
    (P : Sylow p G) : Sylow p G â‰ƒ G â§¸ (P : Subgroup G).normalizer :=
  calc
    Sylow p G â‰ƒ (âŠ¤ : Set (Sylow p G)) := (Equiv.Set.univ (Sylow p G)).symm
    _ â‰ƒ orbit G P := by rw [P.orbit_eq_top]
                        -- ğŸ‰ no goals
    _ â‰ƒ G â§¸ stabilizer G P := (orbitEquivQuotientStabilizer G P)
    _ â‰ƒ G â§¸ (P : Subgroup G).normalizer := by rw [P.stabilizer_eq_normalizer]
                                              -- ğŸ‰ no goals

#align sylow.equiv_quotient_normalizer Sylow.equivQuotientNormalizer

noncomputable instance [Fact p.Prime] [Fintype (Sylow p G)] (P : Sylow p G) :
    Fintype (G â§¸ (P : Subgroup G).normalizer) :=
  ofEquiv (Sylow p G) P.equivQuotientNormalizer

theorem card_sylow_eq_card_quotient_normalizer [Fact p.Prime] [Fintype (Sylow p G)]
    (P : Sylow p G) : card (Sylow p G) = card (G â§¸ (P : Subgroup G).normalizer) :=
  card_congr P.equivQuotientNormalizer
#align card_sylow_eq_card_quotient_normalizer card_sylow_eq_card_quotient_normalizer

theorem card_sylow_eq_index_normalizer [Fact p.Prime] [Fintype (Sylow p G)] (P : Sylow p G) :
    card (Sylow p G) = (P : Subgroup G).normalizer.index :=
  (card_sylow_eq_card_quotient_normalizer P).trans (P : Subgroup G).normalizer.index_eq_card.symm
#align card_sylow_eq_index_normalizer card_sylow_eq_index_normalizer

theorem card_sylow_dvd_index [Fact p.Prime] [Fintype (Sylow p G)] (P : Sylow p G) :
    card (Sylow p G) âˆ£ (P : Subgroup G).index :=
  ((congr_arg _ (card_sylow_eq_index_normalizer P)).mp dvd_rfl).trans
    (index_dvd_of_le le_normalizer)
#align card_sylow_dvd_index card_sylow_dvd_index

theorem not_dvd_index_sylow' [hp : Fact p.Prime] (P : Sylow p G) [(P : Subgroup G).Normal]
    [fP : FiniteIndex (P : Subgroup G)] : Â¬p âˆ£ (P : Subgroup G).index := by
  intro h
  -- âŠ¢ False
  letI : Fintype (G â§¸ (P : Subgroup G)) := (P : Subgroup G).fintypeQuotientOfFiniteIndex
  -- âŠ¢ False
  rw [index_eq_card (P : Subgroup G)] at h
  -- âŠ¢ False
  obtain âŸ¨x, hxâŸ© := exists_prime_orderOf_dvd_card (G := G â§¸ (P : Subgroup G)) p h
  -- âŠ¢ False
  have h := IsPGroup.of_card ((orderOf_eq_card_zpowers.symm.trans hx).trans (pow_one p).symm)
  -- âŠ¢ False
  let Q := (zpowers x).comap (QuotientGroup.mk' (P : Subgroup G))
  -- âŠ¢ False
  have hQ : IsPGroup p Q := by
    apply h.comap_of_ker_isPGroup
    rw [QuotientGroup.ker_mk']
    exact P.2
  replace hp := mt orderOf_eq_one_iff.mpr (ne_of_eq_of_ne hx hp.1.ne_one)
  -- âŠ¢ False
  rw [â† zpowers_eq_bot, â† Ne, â† bot_lt_iff_ne_bot, â†
    comap_lt_comap_of_surjective (QuotientGroup.mk'_surjective _), MonoidHom.comap_bot,
    QuotientGroup.ker_mk'] at hp
  exact hp.ne' (P.3 hQ hp.le)
  -- ğŸ‰ no goals
#align not_dvd_index_sylow' not_dvd_index_sylow'

theorem not_dvd_index_sylow [hp : Fact p.Prime] [Finite (Sylow p G)] (P : Sylow p G)
    (hP : relindex â†‘P (P : Subgroup G).normalizer â‰  0) : Â¬p âˆ£ (P : Subgroup G).index := by
  cases nonempty_fintype (Sylow p G)
  -- âŠ¢ Â¬p âˆ£ index â†‘P
  rw [â† relindex_mul_index le_normalizer, â† card_sylow_eq_index_normalizer]
  -- âŠ¢ Â¬p âˆ£ relindex (â†‘P) (normalizer â†‘P) * card (Sylow p G)
  haveI : (P.subtype le_normalizer : Subgroup (P : Subgroup G).normalizer).Normal :=
    Subgroup.normal_in_normalizer
  haveI : FiniteIndex â†‘(P.subtype le_normalizer : Subgroup (P : Subgroup G).normalizer) := âŸ¨hPâŸ©
  -- âŠ¢ Â¬p âˆ£ relindex (â†‘P) (normalizer â†‘P) * card (Sylow p G)
  replace hP := not_dvd_index_sylow' (P.subtype le_normalizer)
  -- âŠ¢ Â¬p âˆ£ relindex (â†‘P) (normalizer â†‘P) * card (Sylow p G)
  exact hp.1.not_dvd_mul hP (not_dvd_card_sylow p G)
  -- ğŸ‰ no goals
#align not_dvd_index_sylow not_dvd_index_sylow

/-- **Frattini's Argument**: If `N` is a normal subgroup of `G`, and if `P` is a Sylow `p`-subgroup
  of `N`, then `N_G(P) âŠ” N = G`. -/
theorem Sylow.normalizer_sup_eq_top {p : â„•} [Fact p.Prime] {N : Subgroup G} [N.Normal]
    [Finite (Sylow p N)] (P : Sylow p N) :
    ((â†‘P : Subgroup N).map N.subtype).normalizer âŠ” N = âŠ¤ := by
  refine' top_le_iff.mp fun g _ => _
  -- âŠ¢ g âˆˆ normalizer (map (Subgroup.subtype N) â†‘P) âŠ” N
  obtain âŸ¨n, hnâŸ© := exists_smul_eq N ((MulAut.conjNormal g : MulAut N) â€¢ P) P
  -- âŠ¢ g âˆˆ normalizer (map (Subgroup.subtype N) â†‘P) âŠ” N
  rw [â† inv_mul_cancel_left (â†‘n) g, sup_comm]
  -- âŠ¢ (â†‘n)â»Â¹ * (â†‘n * g) âˆˆ N âŠ” normalizer (map (Subgroup.subtype N) â†‘P)
  apply mul_mem_sup (N.inv_mem n.2)
  -- âŠ¢ â†‘n * g âˆˆ normalizer (map (Subgroup.subtype N) â†‘P)
  rw [Sylow.smul_def, â† mul_smul, â† MulAut.conjNormal_val, â† MulAut.conjNormal.map_mul,
    Sylow.ext_iff, Sylow.pointwise_smul_def, Subgroup.pointwise_smul_def] at hn
  refine' fun x =>
    (mem_map_iff_mem
            (show Function.Injective (MulAut.conj (â†‘n * g)).toMonoidHom from
              (MulAut.conj (â†‘n * g)).injective)).symm.trans
      _
  rw [map_map, â† congr_arg (map N.subtype) hn, map_map]
  -- âŠ¢ â†‘(MulEquiv.toMonoidHom (â†‘MulAut.conj (â†‘n * g))) x âˆˆ map (MonoidHom.comp (Mul â€¦
  rfl
  -- ğŸ‰ no goals
#align sylow.normalizer_sup_eq_top Sylow.normalizer_sup_eq_top

/-- **Frattini's Argument**: If `N` is a normal subgroup of `G`, and if `P` is a Sylow `p`-subgroup
  of `N`, then `N_G(P) âŠ” N = G`. -/
theorem Sylow.normalizer_sup_eq_top' {p : â„•} [Fact p.Prime] {N : Subgroup G} [N.Normal]
    [Finite (Sylow p N)] (P : Sylow p G) (hP : â†‘P â‰¤ N) : (P : Subgroup G).normalizer âŠ” N = âŠ¤ := by
  rw [â† Sylow.normalizer_sup_eq_top (P.subtype hP), P.coe_subtype, subgroupOf_map_subtype,
    inf_of_le_left hP]
#align sylow.normalizer_sup_eq_top' Sylow.normalizer_sup_eq_top'

end InfiniteSylow

open Equiv Equiv.Perm Finset Function List QuotientGroup

open BigOperators

universe u v w

variable {G : Type u} {Î± : Type v} {Î² : Type w} [Group G]

attribute [local instance 10] Subtype.fintype setFintype Classical.propDecidable

theorem QuotientGroup.card_preimage_mk [Fintype G] (s : Subgroup G) (t : Set (G â§¸ s)) :
    Fintype.card (QuotientGroup.mk â»Â¹' t) = Fintype.card s * Fintype.card t := by
  rw [â† Fintype.card_prod, Fintype.card_congr (preimageMkEquivSubgroupProdSet _ _)]
  -- ğŸ‰ no goals
#align quotient_group.card_preimage_mk QuotientGroup.card_preimage_mk

namespace Sylow

open Subgroup Submonoid MulAction

theorem mem_fixedPoints_mul_left_cosets_iff_mem_normalizer {H : Subgroup G} [Finite (H : Set G)]
    {x : G} : (x : G â§¸ H) âˆˆ MulAction.fixedPoints H (G â§¸ H) â†” x âˆˆ normalizer H :=
  âŸ¨fun hx =>
    have ha : âˆ€ {y : G â§¸ H}, y âˆˆ orbit H (x : G â§¸ H) â†’ y = x := (mem_fixedPoints' _).1 hx _
    (inv_mem_iff (G := G)).1
      (mem_normalizer_fintype fun n (hn : n âˆˆ H) =>
        have : (nâ»Â¹ * x)â»Â¹ * x âˆˆ H := QuotientGroup.eq.1 (ha âŸ¨âŸ¨nâ»Â¹, inv_mem hnâŸ©, rflâŸ©)
        show _ âˆˆ H by
          rw [mul_inv_rev, inv_inv] at this
          -- âŠ¢ xâ»Â¹ * n * xâ»Â¹â»Â¹ âˆˆ H
          convert this
          -- âŠ¢ xâ»Â¹â»Â¹ = x
          rw [inv_inv]),
          -- ğŸ‰ no goals
    fun hx : âˆ€ n : G, n âˆˆ H â†” x * n * xâ»Â¹ âˆˆ H =>
    (mem_fixedPoints' _).2 fun y =>
      Quotient.inductionOn' y fun y hy =>
        QuotientGroup.eq.2
          (let âŸ¨âŸ¨b, hbâ‚âŸ©, hbâ‚‚âŸ© := hy
          have hbâ‚‚ : (b * x)â»Â¹ * y âˆˆ H := QuotientGroup.eq.1 hbâ‚‚
          (inv_mem_iff (G := G)).1 <|
            (hx _).2 <|
              (mul_mem_cancel_left (inv_mem hbâ‚)).1 <| by
                rw [hx] at hbâ‚‚; simpa [mul_inv_rev, mul_assoc] using hbâ‚‚)âŸ©
                -- âŠ¢ bâ»Â¹ * (x * (yâ»Â¹ * x)â»Â¹ * xâ»Â¹) âˆˆ H
                                -- ğŸ‰ no goals
#align sylow.mem_fixed_points_mul_left_cosets_iff_mem_normalizer Sylow.mem_fixedPoints_mul_left_cosets_iff_mem_normalizer

/-- The fixed points of the action of `H` on its cosets correspond to `normalizer H / H`. -/
def fixedPointsMulLeftCosetsEquivQuotient (H : Subgroup G) [Finite (H : Set G)] :
    MulAction.fixedPoints H (G â§¸ H) â‰ƒ
      normalizer H â§¸ Subgroup.comap ((normalizer H).subtype : normalizer H â†’* G) H :=
  @subtypeQuotientEquivQuotientSubtype G (normalizer H : Set G) (_) (_)
    (MulAction.fixedPoints H (G â§¸ H))
    (fun a => (@mem_fixedPoints_mul_left_cosets_iff_mem_normalizer _ _ _ â€¹_â€º _).symm)
    (by
      intros
      -- âŠ¢ Setoid.r xâœ yâœ â†” â†‘xâœ â‰ˆ â†‘yâœ
      dsimp only [instHasEquiv]
      -- âŠ¢ Setoid.r xâœ yâœ â†” Setoid.r â†‘xâœ â†‘yâœ
      rw [leftRel_apply (Î± := normalizer H), leftRel_apply]
      -- âŠ¢ xâœâ»Â¹ * yâœ âˆˆ Subgroup.comap (Subgroup.subtype (normalizer H)) H â†” (â†‘xâœ)â»Â¹ * â†‘ â€¦
      rfl)
      -- ğŸ‰ no goals
#align sylow.fixed_points_mul_left_cosets_equiv_quotient Sylow.fixedPointsMulLeftCosetsEquivQuotient

/-- If `H` is a `p`-subgroup of `G`, then the index of `H` inside its normalizer is congruent
  mod `p` to the index of `H`.  -/
theorem card_quotient_normalizer_modEq_card_quotient [Fintype G] {p : â„•} {n : â„•} [hp : Fact p.Prime]
    {H : Subgroup G} (hH : Fintype.card H = p ^ n) :
    Fintype.card (normalizer H â§¸ Subgroup.comap ((normalizer H).subtype : normalizer H â†’* G) H) â‰¡
      card (G â§¸ H) [MOD p] := by
  rw [â† Fintype.card_congr (fixedPointsMulLeftCosetsEquivQuotient H)]
  -- âŠ¢ Fintype.card â†‘(MulAction.fixedPoints { x // x âˆˆ H } (G â§¸ H)) â‰¡ Fintype.card  â€¦
  exact ((IsPGroup.of_card hH).card_modEq_card_fixedPoints _).symm
  -- ğŸ‰ no goals
#align sylow.card_quotient_normalizer_modeq_card_quotient Sylow.card_quotient_normalizer_modEq_card_quotient

/-- If `H` is a subgroup of `G` of cardinality `p ^ n`, then the cardinality of the
  normalizer of `H` is congruent mod `p ^ (n + 1)` to the cardinality of `G`.  -/
theorem card_normalizer_modEq_card [Fintype G] {p : â„•} {n : â„•} [hp : Fact p.Prime] {H : Subgroup G}
    (hH : Fintype.card H = p ^ n) : card (normalizer H) â‰¡ card G [MOD p ^ (n + 1)] := by
  have : H.subgroupOf (normalizer H) â‰ƒ H := (subgroupOfEquivOfLe le_normalizer).toEquiv
  -- âŠ¢ Fintype.card { x // x âˆˆ normalizer H } â‰¡ Fintype.card G [MOD p ^ (n + 1)]
  rw [card_eq_card_quotient_mul_card_subgroup H,
    card_eq_card_quotient_mul_card_subgroup (H.subgroupOf (normalizer H)), Fintype.card_congr this,
    hH, pow_succ]
  exact (card_quotient_normalizer_modEq_card_quotient hH).mul_right' _
  -- ğŸ‰ no goals
#align sylow.card_normalizer_modeq_card Sylow.card_normalizer_modEq_card

/-- If `H` is a `p`-subgroup but not a Sylow `p`-subgroup, then `p` divides the
  index of `H` inside its normalizer. -/
theorem prime_dvd_card_quotient_normalizer [Fintype G] {p : â„•} {n : â„•} [hp : Fact p.Prime]
    (hdvd : p ^ (n + 1) âˆ£ card G) {H : Subgroup G} (hH : Fintype.card H = p ^ n) :
    p âˆ£ card (normalizer H â§¸ Subgroup.comap ((normalizer H).subtype : normalizer H â†’* G) H) :=
  let âŸ¨s, hsâŸ© := exists_eq_mul_left_of_dvd hdvd
  have hcard : card (G â§¸ H) = s * p :=
    (mul_left_inj' (show card H â‰  0 from Fintype.card_ne_zero)).1
      (by
        rw [â† card_eq_card_quotient_mul_card_subgroup H, hH, hs, pow_succ', mul_assoc, mul_comm p])
        -- ğŸ‰ no goals
  have hm :
    s * p % p =
      card (normalizer H â§¸ Subgroup.comap ((normalizer H).subtype : normalizer H â†’* G) H) % p :=
    hcard â–¸ (card_quotient_normalizer_modEq_card_quotient hH).symm
  Nat.dvd_of_mod_eq_zero (by rwa [Nat.mod_eq_zero_of_dvd (dvd_mul_left _ _), eq_comm] at hm)
                             -- ğŸ‰ no goals
#align sylow.prime_dvd_card_quotient_normalizer Sylow.prime_dvd_card_quotient_normalizer

/-- If `H` is a `p`-subgroup but not a Sylow `p`-subgroup of cardinality `p ^ n`,
  then `p ^ (n + 1)` divides the cardinality of the normalizer of `H`. -/
theorem prime_pow_dvd_card_normalizer [Fintype G] {p : â„•} {n : â„•} [_hp : Fact p.Prime]
    (hdvd : p ^ (n + 1) âˆ£ card G) {H : Subgroup G} (hH : Fintype.card H = p ^ n) :
    p ^ (n + 1) âˆ£ card (normalizer H) :=
  Nat.modEq_zero_iff_dvd.1 ((card_normalizer_modEq_card hH).trans hdvd.modEq_zero_nat)
#align sylow.prime_pow_dvd_card_normalizer Sylow.prime_pow_dvd_card_normalizer

/-- If `H` is a subgroup of `G` of cardinality `p ^ n`,
  then `H` is contained in a subgroup of cardinality `p ^ (n + 1)`
  if `p ^ (n + 1)` divides the cardinality of `G` -/
theorem exists_subgroup_card_pow_succ [Fintype G] {p : â„•} {n : â„•} [hp : Fact p.Prime]
    (hdvd : p ^ (n + 1) âˆ£ card G) {H : Subgroup G} (hH : Fintype.card H = p ^ n) :
    âˆƒ K : Subgroup G, Fintype.card K = p ^ (n + 1) âˆ§ H â‰¤ K :=
  let âŸ¨s, hsâŸ© := exists_eq_mul_left_of_dvd hdvd
  have hcard : card (G â§¸ H) = s * p :=
    (mul_left_inj' (show card H â‰  0 from Fintype.card_ne_zero)).1
      (by
        rw [â† card_eq_card_quotient_mul_card_subgroup H, hH, hs, pow_succ', mul_assoc, mul_comm p])
        -- ğŸ‰ no goals
  have hm : s * p % p = card (normalizer H â§¸ H.subgroupOf H.normalizer) % p :=
    Fintype.card_congr (fixedPointsMulLeftCosetsEquivQuotient H) â–¸
      hcard â–¸ (IsPGroup.of_card hH).card_modEq_card_fixedPoints _
  have hm' : p âˆ£ card (normalizer H â§¸ H.subgroupOf H.normalizer) :=
    Nat.dvd_of_mod_eq_zero (by rwa [Nat.mod_eq_zero_of_dvd (dvd_mul_left _ _), eq_comm] at hm)
                               -- ğŸ‰ no goals
  let âŸ¨x, hxâŸ© := @exists_prime_orderOf_dvd_card _ (QuotientGroup.Quotient.group _) _ _ hp hm'
  have hequiv : H â‰ƒ H.subgroupOf H.normalizer := (subgroupOfEquivOfLe le_normalizer).symm.toEquiv
  âŸ¨Subgroup.map (normalizer H).subtype
      (Subgroup.comap (mk' (H.subgroupOf H.normalizer)) (zpowers x)), by
    show Fintype.card (Subgroup.map H.normalizer.subtype
              (comap (mk' (H.subgroupOf H.normalizer)) (Subgroup.zpowers x))) = p ^ (n + 1)
    suffices Fintype.card (Subtype.val ''
              (Subgroup.comap (mk' (H.subgroupOf H.normalizer)) (zpowers x) : Set H.normalizer)) =
        p ^ (n + 1)
      by convert this using 2
    rw [Set.card_image_of_injective
        (Subgroup.comap (mk' (H.subgroupOf H.normalizer)) (zpowers x) : Set H.normalizer)
        Subtype.val_injective,
      pow_succ', â† hH, Fintype.card_congr hequiv, â† hx, orderOf_eq_card_zpowers, â†
      Fintype.card_prod]
    exact @Fintype.card_congr _ _ (_) (_)
      (preimageMkEquivSubgroupProdSet (H.subgroupOf H.normalizer) (zpowers x)), by
    intro y hy
    -- âŠ¢ y âˆˆ Subgroup.map (Subgroup.subtype (normalizer H)) (Subgroup.comap (mk' (sub â€¦
    simp only [exists_prop, Subgroup.coeSubtype, mk'_apply, Subgroup.mem_map, Subgroup.mem_comap]
    -- âŠ¢ âˆƒ x_1, â†‘x_1 âˆˆ zpowers x âˆ§ â†‘x_1 = y
    refine' âŸ¨âŸ¨y, le_normalizer hyâŸ©, âŸ¨0, _âŸ©, rflâŸ©
    -- âŠ¢ (fun x x_1 => x ^ x_1) x 0 = â†‘{ val := y, property := (_ : y âˆˆ normalizer H) }
    dsimp only
    -- âŠ¢ x ^ 0 = â†‘{ val := y, property := (_ : y âˆˆ normalizer H) }
    rw [zpow_zero, eq_comm, QuotientGroup.eq_one_iff]
    -- âŠ¢ { val := y, property := (_ : y âˆˆ normalizer H) } âˆˆ subgroupOf H (normalizer H)
    simpa using hyâŸ©
    -- ğŸ‰ no goals
#align sylow.exists_subgroup_card_pow_succ Sylow.exists_subgroup_card_pow_succ

/-- If `H` is a subgroup of `G` of cardinality `p ^ n`,
  then `H` is contained in a subgroup of cardinality `p ^ m`
  if `n â‰¤ m` and `p ^ m` divides the cardinality of `G` -/
theorem exists_subgroup_card_pow_prime_le [Fintype G] (p : â„•) :
    âˆ€ {n m : â„•} [_hp : Fact p.Prime] (_hdvd : p ^ m âˆ£ card G) (H : Subgroup G)
      (_hH : card H = p ^ n) (_hnm : n â‰¤ m), âˆƒ K : Subgroup G, card K = p ^ m âˆ§ H â‰¤ K
  | n, m => fun {hdvd H hH hnm} =>
    (lt_or_eq_of_le hnm).elim
      (fun hnm : n < m =>
        have h0m : 0 < m := lt_of_le_of_lt n.zero_le hnm
        have _wf : m - 1 < m := Nat.sub_lt h0m zero_lt_one
        have hnm1 : n â‰¤ m - 1 := le_tsub_of_add_le_right hnm
        let âŸ¨K, hKâŸ© :=
          @exists_subgroup_card_pow_prime_le _ _ n (m - 1) _
            (Nat.pow_dvd_of_le_of_pow_dvd tsub_le_self hdvd) H hH hnm1
        have hdvd' : p ^ (m - 1 + 1) âˆ£ card G := by rwa [tsub_add_cancel_of_le h0m.nat_succ_le]
                                                    -- ğŸ‰ no goals
        let âŸ¨K', hK'âŸ© := @exists_subgroup_card_pow_succ _ _ _ _ _ _ hdvd' K hK.1
        âŸ¨K', by rw [hK'.1, tsub_add_cancel_of_le h0m.nat_succ_le], le_trans hK.2 hK'.2âŸ©)
                -- ğŸ‰ no goals
      fun hnm : n = m => âŸ¨H, by simp [hH, hnm]âŸ©
                                -- ğŸ‰ no goals
#align sylow.exists_subgroup_card_pow_prime_le Sylow.exists_subgroup_card_pow_prime_le

/-- A generalisation of **Sylow's first theorem**. If `p ^ n` divides
  the cardinality of `G`, then there is a subgroup of cardinality `p ^ n` -/
theorem exists_subgroup_card_pow_prime [Fintype G] (p : â„•) {n : â„•} [Fact p.Prime]
    (hdvd : p ^ n âˆ£ card G) : âˆƒ K : Subgroup G, Fintype.card K = p ^ n :=
  let âŸ¨K, hKâŸ© := exists_subgroup_card_pow_prime_le p hdvd âŠ¥ (card_bot.trans (by simp)) n.zero_le
                                                                                -- ğŸ‰ no goals
  âŸ¨K, hK.1âŸ©
#align sylow.exists_subgroup_card_pow_prime Sylow.exists_subgroup_card_pow_prime

theorem pow_dvd_card_of_pow_dvd_card [Fintype G] {p n : â„•} [hp : Fact p.Prime] (P : Sylow p G)
    (hdvd : p ^ n âˆ£ card G) : p ^ n âˆ£ card P :=
  (hp.1.coprime_pow_of_not_dvd
          (not_dvd_index_sylow P index_ne_zero_of_finite)).symm.dvd_of_dvd_mul_left
    ((index_mul_card P.1).symm â–¸ hdvd)
#align sylow.pow_dvd_card_of_pow_dvd_card Sylow.pow_dvd_card_of_pow_dvd_card

theorem dvd_card_of_dvd_card [Fintype G] {p : â„•} [Fact p.Prime] (P : Sylow p G)
    (hdvd : p âˆ£ card G) : p âˆ£ card P := by
  rw [â† pow_one p] at hdvd
  -- âŠ¢ p âˆ£ Fintype.card { x // x âˆˆ â†‘P }
  have key := P.pow_dvd_card_of_pow_dvd_card hdvd
  -- âŠ¢ p âˆ£ Fintype.card { x // x âˆˆ â†‘P }
  rwa [pow_one] at key
  -- ğŸ‰ no goals
#align sylow.dvd_card_of_dvd_card Sylow.dvd_card_of_dvd_card

/-- Sylow subgroups are Hall subgroups. -/
theorem card_coprime_index [Fintype G] {p : â„•} [hp : Fact p.Prime] (P : Sylow p G) :
    (card P).coprime (index (P : Subgroup G)) :=
  let âŸ¨_n, hnâŸ© := IsPGroup.iff_card.mp P.2
  hn.symm â–¸ (hp.1.coprime_pow_of_not_dvd (not_dvd_index_sylow P index_ne_zero_of_finite)).symm
#align sylow.card_coprime_index Sylow.card_coprime_index

theorem ne_bot_of_dvd_card [Fintype G] {p : â„•} [hp : Fact p.Prime] (P : Sylow p G)
    (hdvd : p âˆ£ card G) : (P : Subgroup G) â‰  âŠ¥ := by
  refine' fun h => hp.out.not_dvd_one _
  -- âŠ¢ p âˆ£ 1
  have key : p âˆ£ card (P : Subgroup G) := P.dvd_card_of_dvd_card hdvd
  -- âŠ¢ p âˆ£ 1
  rwa [h, card_bot] at key
  -- ğŸ‰ no goals
#align sylow.ne_bot_of_dvd_card Sylow.ne_bot_of_dvd_card

/-- The cardinality of a Sylow subgroup is `p ^ n`
 where `n` is the multiplicity of `p` in the group order. -/
theorem card_eq_multiplicity [Fintype G] {p : â„•} [hp : Fact p.Prime] (P : Sylow p G) :
    card P = p ^ Nat.factorization (card G) p := by
  obtain âŸ¨n, heq : card P = _âŸ© := IsPGroup.iff_card.mp P.isPGroup'
  -- âŠ¢ Fintype.card { x // x âˆˆ â†‘P } = p ^ â†‘(Nat.factorization (Fintype.card G)) p
  refine' Nat.dvd_antisymm _ (P.pow_dvd_card_of_pow_dvd_card (Nat.ord_proj_dvd _ p))
  -- âŠ¢ Fintype.card { x // x âˆˆ â†‘P } âˆ£ p ^ â†‘(Nat.factorization (Fintype.card G)) p
  rw [heq, â† hp.out.pow_dvd_iff_dvd_ord_proj (show card G â‰  0 from card_ne_zero), â† heq]
  -- âŠ¢ Fintype.card { x // x âˆˆ â†‘P } âˆ£ Fintype.card G
  exact P.1.card_subgroup_dvd_card
  -- ğŸ‰ no goals
#align sylow.card_eq_multiplicity Sylow.card_eq_multiplicity

/-- A subgroup with cardinality `p ^ n` is a Sylow subgroup
 where `n` is the multiplicity of `p` in the group order. -/
def ofCard [Fintype G] {p : â„•} [Fact p.Prime] (H : Subgroup G) [Fintype H]
    (card_eq : card H = p ^ (card G).factorization p) : Sylow p G
    where
  toSubgroup := H
  isPGroup' := IsPGroup.of_card card_eq
  is_maximal' := by
    obtain âŸ¨P, hHPâŸ© := (IsPGroup.of_card card_eq).exists_le_sylow
    -- âŠ¢ âˆ€ {Q : Subgroup G}, IsPGroup p { x // x âˆˆ Q } â†’ H â‰¤ Q â†’ Q = H
    exact SetLike.ext'
      (Set.eq_of_subset_of_card_le hHP (P.card_eq_multiplicity.trans card_eq.symm).le).symm â–¸ P.3
#align sylow.of_card Sylow.ofCard

@[simp, norm_cast]
theorem coe_ofCard [Fintype G] {p : â„•} [Fact p.Prime] (H : Subgroup G) [Fintype H]
    (card_eq : card H = p ^ (card G).factorization p) : â†‘(ofCard H card_eq) = H :=
  rfl
#align sylow.coe_of_card Sylow.coe_ofCard

theorem subsingleton_of_normal {p : â„•} [Fact p.Prime] [Finite (Sylow p G)] (P : Sylow p G)
    (h : (P : Subgroup G).Normal) : Subsingleton (Sylow p G) := by
  apply Subsingleton.intro
  -- âŠ¢ âˆ€ (a b : Sylow p G), a = b
  intro Q R
  -- âŠ¢ Q = R
  obtain âŸ¨x, h1âŸ© := exists_smul_eq G P Q
  -- âŠ¢ Q = R
  obtain âŸ¨x, h2âŸ© := exists_smul_eq G P R
  -- âŠ¢ Q = R
  rw [Sylow.smul_eq_of_normal] at h1 h2
  -- âŠ¢ Q = R
  rw [â† h1, â† h2]
  -- ğŸ‰ no goals
#align sylow.subsingleton_of_normal Sylow.subsingleton_of_normal

section Pointwise

open Pointwise

theorem characteristic_of_normal {p : â„•} [Fact p.Prime] [Finite (Sylow p G)] (P : Sylow p G)
    (h : (P : Subgroup G).Normal) : (P : Subgroup G).Characteristic := by
  haveI := Sylow.subsingleton_of_normal P h
  -- âŠ¢ Characteristic â†‘P
  rw [characteristic_iff_map_eq]
  -- âŠ¢ âˆ€ (Ï• : G â‰ƒ* G), Subgroup.map (MulEquiv.toMonoidHom Ï•) â†‘P = â†‘P
  intro Î¦
  -- âŠ¢ Subgroup.map (MulEquiv.toMonoidHom Î¦) â†‘P = â†‘P
  show (Î¦ â€¢ P).toSubgroup = P.toSubgroup
  -- âŠ¢ â†‘(Î¦ â€¢ P) = â†‘P
  congr
  -- âŠ¢ Î¦ â€¢ P = P
  simp
  -- ğŸ‰ no goals
#align sylow.characteristic_of_normal Sylow.characteristic_of_normal

end Pointwise

theorem normal_of_normalizer_normal {p : â„•} [Fact p.Prime] [Finite (Sylow p G)] (P : Sylow p G)
    (hn : (â†‘P : Subgroup G).normalizer.Normal) : (â†‘P : Subgroup G).Normal := by
  rw [â† normalizer_eq_top, â† normalizer_sup_eq_top' P le_normalizer, sup_idem]
  -- ğŸ‰ no goals
#align sylow.normal_of_normalizer_normal Sylow.normal_of_normalizer_normal

@[simp]
theorem normalizer_normalizer {p : â„•} [Fact p.Prime] [Finite (Sylow p G)] (P : Sylow p G) :
    (â†‘P : Subgroup G).normalizer.normalizer = (â†‘P : Subgroup G).normalizer := by
  have := normal_of_normalizer_normal (P.subtype (le_normalizer.trans le_normalizer))
  -- âŠ¢ normalizer (normalizer â†‘P) = normalizer â†‘P
  simp_rw [â† normalizer_eq_top, Sylow.coe_subtype, â† subgroupOf_normalizer_eq le_normalizer, â†
    subgroupOf_normalizer_eq le_rfl, subgroupOf_self] at this
  rw [â† subtype_range (P : Subgroup G).normalizer.normalizer, MonoidHom.range_eq_map,
    â† this trivial]
  exact map_comap_eq_self (le_normalizer.trans (ge_of_eq (subtype_range _)))
  -- ğŸ‰ no goals
#align sylow.normalizer_normalizer Sylow.normalizer_normalizer

theorem normal_of_all_max_subgroups_normal [Finite G]
    (hnc : âˆ€ H : Subgroup G, IsCoatom H â†’ H.Normal) {p : â„•} [Fact p.Prime] [Finite (Sylow p G)]
    (P : Sylow p G) : (â†‘P : Subgroup G).Normal :=
  normalizer_eq_top.mp
    (by
      rcases eq_top_or_exists_le_coatom (â†‘P : Subgroup G).normalizer with (heq | âŸ¨K, hK, hNKâŸ©)
      -- âŠ¢ normalizer â†‘P = âŠ¤
      Â· exact heq
        -- ğŸ‰ no goals
      Â· haveI := hnc _ hK
        -- âŠ¢ normalizer â†‘P = âŠ¤
        have hPK : â†‘P â‰¤ K := le_trans le_normalizer hNK
        -- âŠ¢ normalizer â†‘P = âŠ¤
        refine' (hK.1 _).elim
        -- âŠ¢ K = âŠ¤
        rw [â† sup_of_le_right hNK, P.normalizer_sup_eq_top' hPK])
        -- ğŸ‰ no goals
#align sylow.normal_of_all_max_subgroups_normal Sylow.normal_of_all_max_subgroups_normal

theorem normal_of_normalizerCondition (hnc : NormalizerCondition G) {p : â„•} [Fact p.Prime]
    [Finite (Sylow p G)] (P : Sylow p G) : (â†‘P : Subgroup G).Normal :=
  normalizer_eq_top.mp <|
    normalizerCondition_iff_only_full_group_self_normalizing.mp hnc _ <| normalizer_normalizer _
#align sylow.normal_of_normalizer_condition Sylow.normal_of_normalizerCondition

open BigOperators

/-- If all its Sylow subgroups are normal, then a finite group is isomorphic to the direct product
of these Sylow subgroups.
-/
noncomputable def directProductOfNormal [Fintype G]
    (hn : âˆ€ {p : â„•} [Fact p.Prime] (P : Sylow p G), (â†‘P : Subgroup G).Normal) :
    (âˆ€ p : (card G).factorization.support, âˆ€ P : Sylow p G, (â†‘P : Subgroup G)) â‰ƒ* G := by
  set ps := (Fintype.card G).factorization.support
  -- âŠ¢ ((p : { x // x âˆˆ ps }) â†’ (P : Sylow (â†‘p) G) â†’ { x // x âˆˆ â†‘P }) â‰ƒ* G
  -- â€œTheâ€ Sylow subgroup for p
  let P : âˆ€ p, Sylow p G := default
  -- âŠ¢ ((p : { x // x âˆˆ ps }) â†’ (P : Sylow (â†‘p) G) â†’ { x // x âˆˆ â†‘P }) â‰ƒ* G
  have hcomm : Pairwise fun pâ‚ pâ‚‚ : ps => âˆ€ x y : G, x âˆˆ P pâ‚ â†’ y âˆˆ P pâ‚‚ â†’ Commute x y := by
    rintro âŸ¨pâ‚, hpâ‚âŸ© âŸ¨pâ‚‚, hpâ‚‚âŸ© hne
    haveI hpâ‚' := Fact.mk (Nat.prime_of_mem_factorization hpâ‚)
    haveI hpâ‚‚' := Fact.mk (Nat.prime_of_mem_factorization hpâ‚‚)
    have hne' : pâ‚ â‰  pâ‚‚ := by simpa using hne
    apply Subgroup.commute_of_normal_of_disjoint _ _ (hn (P pâ‚)) (hn (P pâ‚‚))
    apply IsPGroup.disjoint_of_ne pâ‚ pâ‚‚ hne' _ _ (P pâ‚).isPGroup' (P pâ‚‚).isPGroup'
  refine' MulEquiv.trans (N := âˆ€ p : ps, P p) _ _
  -- âŠ¢ ((p : { x // x âˆˆ ps }) â†’ (P : Sylow (â†‘p) G) â†’ { x // x âˆˆ â†‘P }) â‰ƒ* ((p : { x  â€¦
  -- There is only one Sylow subgroup for each p, so the inner product is trivial
  show (âˆ€ p : ps, âˆ€ P : Sylow p G, P) â‰ƒ* âˆ€ p : ps, P p
  -- âŠ¢ ((p : { x // x âˆˆ ps }) â†’ (P : Sylow (â†‘p) G) â†’ { x // x âˆˆ P }) â‰ƒ* ((p : { x / â€¦
  Â· -- here we need to help the elaborator with an explicit instantiation
    apply @MulEquiv.piCongrRight ps (fun p => âˆ€ P : Sylow p G, P) (fun p => P p) _ _
    -- âŠ¢ (j : { x // x âˆˆ ps }) â†’ ((P : Sylow (â†‘j) G) â†’ { x // x âˆˆ P }) â‰ƒ* { x // x âˆˆ  â€¦
    rintro âŸ¨p, hpâŸ©
    -- âŠ¢ ((P : Sylow (â†‘{ val := p, property := hp }) G) â†’ { x // x âˆˆ P }) â‰ƒ* { x // x â€¦
    haveI hp' := Fact.mk (Nat.prime_of_mem_factorization hp)
    -- âŠ¢ ((P : Sylow (â†‘{ val := p, property := hp }) G) â†’ { x // x âˆˆ P }) â‰ƒ* { x // x â€¦
    haveI := subsingleton_of_normal _ (hn (P p))
    -- âŠ¢ ((P : Sylow (â†‘{ val := p, property := hp }) G) â†’ { x // x âˆˆ P }) â‰ƒ* { x // x â€¦
    change (âˆ€ P : Sylow p G, P) â‰ƒ* P p
    -- âŠ¢ ((P : Sylow p G) â†’ { x // x âˆˆ P }) â‰ƒ* { x // x âˆˆ â†‘(P p) }
    exact MulEquiv.piSubsingleton _ _
    -- ğŸ‰ no goals
  show (âˆ€ p : ps, P p) â‰ƒ* G
  -- âŠ¢ ((p : { x // x âˆˆ ps }) â†’ { x // x âˆˆ P â†‘p }) â‰ƒ* G
  apply MulEquiv.ofBijective (Subgroup.noncommPiCoprod hcomm)
  -- âŠ¢ Bijective â†‘(noncommPiCoprod hcomm)
  apply (bijective_iff_injective_and_card _).mpr
  -- âŠ¢ Injective â†‘(noncommPiCoprod hcomm) âˆ§ Fintype.card ((i : { x // x âˆˆ ps }) â†’ { â€¦
  constructor
  -- âŠ¢ Injective â†‘(noncommPiCoprod hcomm)
  show Injective _
  -- âŠ¢ Injective â†‘(noncommPiCoprod hcomm)
  Â· apply Subgroup.injective_noncommPiCoprod_of_independent
    -- âŠ¢ CompleteLattice.Independent fun i => â†‘(P â†‘i)
    apply independent_of_coprime_order hcomm
    -- âŠ¢ âˆ€ (i j : { x // x âˆˆ ps }), i â‰  j â†’ Nat.coprime (Fintype.card { x // x âˆˆ â†‘(P  â€¦
    rintro âŸ¨pâ‚, hpâ‚âŸ© âŸ¨pâ‚‚, hpâ‚‚âŸ© hne
    -- âŠ¢ Nat.coprime (Fintype.card { x // x âˆˆ â†‘(P â†‘{ val := pâ‚, property := hpâ‚ }) }) â€¦
    haveI hpâ‚' := Fact.mk (Nat.prime_of_mem_factorization hpâ‚)
    -- âŠ¢ Nat.coprime (Fintype.card { x // x âˆˆ â†‘(P â†‘{ val := pâ‚, property := hpâ‚ }) }) â€¦
    haveI hpâ‚‚' := Fact.mk (Nat.prime_of_mem_factorization hpâ‚‚)
    -- âŠ¢ Nat.coprime (Fintype.card { x // x âˆˆ â†‘(P â†‘{ val := pâ‚, property := hpâ‚ }) }) â€¦
    have hne' : pâ‚ â‰  pâ‚‚ := by simpa using hne
    -- âŠ¢ Nat.coprime (Fintype.card { x // x âˆˆ â†‘(P â†‘{ val := pâ‚, property := hpâ‚ }) }) â€¦
    apply IsPGroup.coprime_card_of_ne pâ‚ pâ‚‚ hne' _ _ (P pâ‚).isPGroup' (P pâ‚‚).isPGroup'
    -- ğŸ‰ no goals
  show card (âˆ€ p : ps, P p) = card G
  -- âŠ¢ Fintype.card ((p : { x // x âˆˆ ps }) â†’ { x // x âˆˆ P â†‘p }) = Fintype.card G
  Â· calc
      card (âˆ€ p : ps, P p) = âˆ p : ps, card (P p) := Fintype.card_pi
      _ = âˆ p : ps, p.1 ^ (card G).factorization p.1 := by
        congr 1 with âŸ¨p, hpâŸ©
        exact @card_eq_multiplicity _ _ _ p âŸ¨Nat.prime_of_mem_factorization hpâŸ© (P p)
      _ = âˆ p in ps, p ^ (card G).factorization p :=
        (Finset.prod_finset_coe (fun p => p ^ (card G).factorization p) _)
      _ = (card G).factorization.prod (Â· ^ Â·) := rfl
      _ = card G := Nat.factorization_prod_pow_eq_self Fintype.card_ne_zero

#align sylow.direct_product_of_normal Sylow.directProductOfNormal

end Sylow
