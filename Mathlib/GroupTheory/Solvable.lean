/-
Copyright (c) 2021 Jordan Brown, Thomas Browning, Patrick Lutz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jordan Brown, Thomas Browning, Patrick Lutz
-/
import Mathlib.Data.Fin.VecNotation
import Mathlib.GroupTheory.Abelianization
import Mathlib.GroupTheory.Perm.ViaEmbedding
import Mathlib.GroupTheory.Subgroup.Simple
import Mathlib.SetTheory.Cardinal.Basic

#align_import group_theory.solvable from "leanprover-community/mathlib"@"dc6c365e751e34d100e80fe6e314c3c3e0fd2988"

/-!
# Solvable Groups

In this file we introduce the notion of a solvable group. We define a solvable group as one whose
derived series is eventually trivial. This requires defining the commutator of two subgroups and
the derived series of a group.

## Main definitions

* `derivedSeries G n` : the `n`th term in the derived series of `G`, defined by iterating
    `general_commutator` starting with the top subgroup
* `IsSolvable G` : the group `G` is solvable
-/


open Subgroup

variable {G G' : Type*} [Group G] [Group G'] {f : G â†’* G'}

section derivedSeries

variable (G)

/-- The derived series of the group `G`, obtained by starting from the subgroup `âŠ¤` and repeatedly
  taking the commutator of the previous subgroup with itself for `n` times. -/
def derivedSeries : â„• â†’ Subgroup G
  | 0 => âŠ¤
  | n + 1 => â…derivedSeries n, derivedSeries nâ†
#align derived_series derivedSeries

@[simp]
theorem derivedSeries_zero : derivedSeries G 0 = âŠ¤ :=
  rfl
#align derived_series_zero derivedSeries_zero

@[simp]
theorem derivedSeries_succ (n : â„•) :
    derivedSeries G (n + 1) = â…derivedSeries G n, derivedSeries G nâ† :=
  rfl
#align derived_series_succ derivedSeries_succ

-- porting note: had to provide inductive hypothesis explicitly
theorem derivedSeries_normal (n : â„•) : (derivedSeries G n).Normal := by
  induction' n with n ih
  -- âŠ¢ Normal (derivedSeries G Nat.zero)
  Â· exact (âŠ¤ : Subgroup G).normal_of_characteristic
    -- ğŸ‰ no goals
  Â· exact @Subgroup.commutator_normal G _ (derivedSeries G n) (derivedSeries G n) ih ih
    -- ğŸ‰ no goals
#align derived_series_normal derivedSeries_normal

-- porting note: higher simp priority to restore Lean 3 behavior
@[simp 1100]
theorem derivedSeries_one : derivedSeries G 1 = commutator G :=
  rfl
#align derived_series_one derivedSeries_one

end derivedSeries

section CommutatorMap

section DerivedSeriesMap

variable (f)

theorem map_derivedSeries_le_derivedSeries (n : â„•) :
    (derivedSeries G n).map f â‰¤ derivedSeries G' n := by
  induction' n with n ih
  -- âŠ¢ map f (derivedSeries G Nat.zero) â‰¤ derivedSeries G' Nat.zero
  Â· exact le_top
    -- ğŸ‰ no goals
  Â· simp only [derivedSeries_succ, map_commutator, commutator_mono, ih]
    -- ğŸ‰ no goals
#align map_derived_series_le_derived_series map_derivedSeries_le_derivedSeries

variable {f}

theorem derivedSeries_le_map_derivedSeries (hf : Function.Surjective f) (n : â„•) :
    derivedSeries G' n â‰¤ (derivedSeries G n).map f := by
  induction' n with n ih
  -- âŠ¢ derivedSeries G' Nat.zero â‰¤ map f (derivedSeries G Nat.zero)
  Â· exact (map_top_of_surjective f hf).ge
    -- ğŸ‰ no goals
  Â· exact commutator_le_map_commutator ih ih
    -- ğŸ‰ no goals
#align derived_series_le_map_derived_series derivedSeries_le_map_derivedSeries

theorem map_derivedSeries_eq (hf : Function.Surjective f) (n : â„•) :
    (derivedSeries G n).map f = derivedSeries G' n :=
  le_antisymm (map_derivedSeries_le_derivedSeries f n) (derivedSeries_le_map_derivedSeries hf n)
#align map_derived_series_eq map_derivedSeries_eq

end DerivedSeriesMap

end CommutatorMap

section Solvable

variable (G)

/-- A group `G` is solvable if its derived series is eventually trivial. We use this definition
  because it's the most convenient one to work with. -/
class IsSolvable : Prop where
  /-- A group `G` is solvable if its derived series is eventually trivial. -/
  solvable : âˆƒ n : â„•, derivedSeries G n = âŠ¥
#align is_solvable IsSolvable

theorem isSolvable_def : IsSolvable G â†” âˆƒ n : â„•, derivedSeries G n = âŠ¥ :=
  âŸ¨fun h => h.solvable, fun h => âŸ¨hâŸ©âŸ©
#align is_solvable_def isSolvable_def

instance (priority := 100) CommGroup.isSolvable {G : Type*} [CommGroup G] : IsSolvable G :=
  âŸ¨âŸ¨1, le_bot_iff.mp (Abelianization.commutator_subset_ker (MonoidHom.id G))âŸ©âŸ©
#align comm_group.is_solvable CommGroup.isSolvable

theorem isSolvable_of_comm {G : Type*} [hG : Group G] (h : âˆ€ a b : G, a * b = b * a) :
    IsSolvable G := by
  letI hG' : CommGroup G := { hG with mul_comm := h }
  -- âŠ¢ IsSolvable G
  cases hG
  -- âŠ¢ IsSolvable G
  exact CommGroup.isSolvable
  -- ğŸ‰ no goals
#align is_solvable_of_comm isSolvable_of_comm

theorem isSolvable_of_top_eq_bot (h : (âŠ¤ : Subgroup G) = âŠ¥) : IsSolvable G :=
  âŸ¨âŸ¨0, hâŸ©âŸ©
#align is_solvable_of_top_eq_bot isSolvable_of_top_eq_bot

instance (priority := 100) isSolvable_of_subsingleton [Subsingleton G] : IsSolvable G :=
  isSolvable_of_top_eq_bot G (by simp)
                                 -- ğŸ‰ no goals
#align is_solvable_of_subsingleton isSolvable_of_subsingleton

variable {G}

theorem solvable_of_ker_le_range {G' G'' : Type*} [Group G'] [Group G''] (f : G' â†’* G)
    (g : G â†’* G'') (hfg : g.ker â‰¤ f.range) [hG' : IsSolvable G'] [hG'' : IsSolvable G''] :
    IsSolvable G := by
  obtain âŸ¨n, hnâŸ© := id hG''
  -- âŠ¢ IsSolvable G
  obtain âŸ¨m, hmâŸ© := id hG'
  -- âŠ¢ IsSolvable G
  refine' âŸ¨âŸ¨n + m, le_bot_iff.mp (map_bot f â–¸ hm â–¸ _)âŸ©âŸ©
  -- âŠ¢ derivedSeries G (n + m) â‰¤ map f (derivedSeries G' m)
  clear hm
  -- âŠ¢ derivedSeries G (n + m) â‰¤ map f (derivedSeries G' m)
  induction' m with m hm
  -- âŠ¢ derivedSeries G (n + Nat.zero) â‰¤ map f (derivedSeries G' Nat.zero)
  Â· exact f.range_eq_map â–¸ ((derivedSeries G n).map_eq_bot_iff.mp
      (le_bot_iff.mp ((map_derivedSeries_le_derivedSeries g n).trans hn.le))).trans hfg
  Â· exact commutator_le_map_commutator hm hm
    -- ğŸ‰ no goals
#align solvable_of_ker_le_range solvable_of_ker_le_range

theorem solvable_of_solvable_injective (hf : Function.Injective f) [IsSolvable G'] :
    IsSolvable G :=
  solvable_of_ker_le_range (1 : G' â†’* G) f ((f.ker_eq_bot_iff.mpr hf).symm â–¸ bot_le)
#align solvable_of_solvable_injective solvable_of_solvable_injective

instance subgroup_solvable_of_solvable (H : Subgroup G) [IsSolvable G] : IsSolvable H :=
  solvable_of_solvable_injective H.subtype_injective
#align subgroup_solvable_of_solvable subgroup_solvable_of_solvable

theorem solvable_of_surjective (hf : Function.Surjective f) [IsSolvable G] : IsSolvable G' :=
  solvable_of_ker_le_range f (1 : G' â†’* G) ((f.range_top_of_surjective hf).symm â–¸ le_top)
#align solvable_of_surjective solvable_of_surjective

instance solvable_quotient_of_solvable (H : Subgroup G) [H.Normal] [IsSolvable G] :
    IsSolvable (G â§¸ H) :=
  solvable_of_surjective (QuotientGroup.mk'_surjective H)
#align solvable_quotient_of_solvable solvable_quotient_of_solvable

instance solvable_prod {G' : Type*} [Group G'] [IsSolvable G] [IsSolvable G'] :
    IsSolvable (G Ã— G') :=
  solvable_of_ker_le_range (MonoidHom.inl G G') (MonoidHom.snd G G') fun x hx =>
    âŸ¨x.1, Prod.ext rfl hx.symmâŸ©
#align solvable_prod solvable_prod

end Solvable

section IsSimpleGroup

variable [IsSimpleGroup G]

theorem IsSimpleGroup.derivedSeries_succ {n : â„•} : derivedSeries G n.succ = commutator G := by
  induction' n with n ih
  -- âŠ¢ derivedSeries G (Nat.succ Nat.zero) = _root_.commutator G
  Â· exact derivedSeries_one G
    -- ğŸ‰ no goals
  rw [_root_.derivedSeries_succ, ih, _root_.commutator]
  -- âŠ¢ â…â…âŠ¤, âŠ¤â†, â…âŠ¤, âŠ¤â†â† = â…âŠ¤, âŠ¤â†
  cases' (commutator_normal (âŠ¤ : Subgroup G) (âŠ¤ : Subgroup G)).eq_bot_or_eq_top with h h
  -- âŠ¢ â…â…âŠ¤, âŠ¤â†, â…âŠ¤, âŠ¤â†â† = â…âŠ¤, âŠ¤â†
  Â· rw [h, commutator_bot_left]
    -- ğŸ‰ no goals
  Â· rwa [h]
    -- ğŸ‰ no goals
#align is_simple_group.derived_series_succ IsSimpleGroup.derivedSeries_succ

theorem IsSimpleGroup.comm_iff_isSolvable : (âˆ€ a b : G, a * b = b * a) â†” IsSolvable G :=
  âŸ¨isSolvable_of_comm, fun âŸ¨âŸ¨n, hnâŸ©âŸ© => by
    cases n
    -- âŠ¢ âˆ€ (a b : G), a * b = b * a
    Â· intro a b
      -- âŠ¢ a * b = b * a
      refine' (mem_bot.1 _).trans (mem_bot.1 _).symm <;>
      -- âŠ¢ a * b âˆˆ âŠ¥
        Â· rw [â† hn]
          -- âŠ¢ a * b âˆˆ derivedSeries G Nat.zero
          -- âŠ¢ b * a âˆˆ derivedSeries G Nat.zero
          -- ğŸ‰ no goals
          exact mem_top _
          -- ğŸ‰ no goals
    Â· rw [IsSimpleGroup.derivedSeries_succ] at hn
      -- âŠ¢ âˆ€ (a b : G), a * b = b * a
      intro a b
      -- âŠ¢ a * b = b * a
      rw [â† mul_inv_eq_one, mul_inv_rev, â† mul_assoc, â† mem_bot, â† hn, commutator_eq_closure]
      -- âŠ¢ a * b * aâ»Â¹ * bâ»Â¹ âˆˆ closure (commutatorSet G)
      exact subset_closure âŸ¨a, b, rflâŸ©âŸ©
      -- ğŸ‰ no goals
#align is_simple_group.comm_iff_is_solvable IsSimpleGroup.comm_iff_isSolvable

end IsSimpleGroup

section PermNotSolvable

theorem not_solvable_of_mem_derivedSeries {g : G} (h1 : g â‰  1)
    (h2 : âˆ€ n : â„•, g âˆˆ derivedSeries G n) : Â¬IsSolvable G :=
  mt (isSolvable_def _).mp
    (not_exists_of_forall_not fun n h =>
      h1 (Subgroup.mem_bot.mp ((congr_arg (Membership.mem g) h).mp (h2 n))))
#align not_solvable_of_mem_derived_series not_solvable_of_mem_derivedSeries

theorem Equiv.Perm.fin_5_not_solvable : Â¬IsSolvable (Equiv.Perm (Fin 5)) := by
  let x : Equiv.Perm (Fin 5) := âŸ¨![1, 2, 0, 3, 4], ![2, 0, 1, 3, 4], by decide, by decideâŸ©
  -- âŠ¢ Â¬IsSolvable (Perm (Fin 5))
  let y : Equiv.Perm (Fin 5) := âŸ¨![3, 4, 2, 0, 1], ![3, 4, 2, 0, 1], by decide, by decideâŸ©
  -- âŠ¢ Â¬IsSolvable (Perm (Fin 5))
  let z : Equiv.Perm (Fin 5) := âŸ¨![0, 3, 2, 1, 4], ![0, 3, 2, 1, 4], by decide, by decideâŸ©
  -- âŠ¢ Â¬IsSolvable (Perm (Fin 5))
  have key : x = z * â…x, y * x * yâ»Â¹â† * zâ»Â¹ := by decide
  -- âŠ¢ Â¬IsSolvable (Perm (Fin 5))
  refine' not_solvable_of_mem_derivedSeries (show x â‰  1 by decide) fun n => _
  -- âŠ¢ x âˆˆ derivedSeries (Perm (Fin 5)) n
  induction' n with n ih
  -- âŠ¢ x âˆˆ derivedSeries (Perm (Fin 5)) Nat.zero
  Â· exact mem_top x
    -- ğŸ‰ no goals
  Â· rw [key, (derivedSeries_normal _ _).mem_comm_iff, inv_mul_cancel_left]
    -- âŠ¢ â…x, y * x * yâ»Â¹â† âˆˆ derivedSeries (Perm (Fin 5)) (Nat.succ n)
    exact commutator_mem_commutator ih ((derivedSeries_normal _ _).conj_mem _ ih _)
    -- ğŸ‰ no goals
#align equiv.perm.fin_5_not_solvable Equiv.Perm.fin_5_not_solvable

theorem Equiv.Perm.not_solvable (X : Type*) (hX : 5 â‰¤ Cardinal.mk X) :
    Â¬IsSolvable (Equiv.Perm X) := by
  intro h
  -- âŠ¢ False
  have key : Nonempty (Fin 5 â†ª X) := by
    rwa [â† Cardinal.lift_mk_le, Cardinal.mk_fin, Cardinal.lift_natCast, Cardinal.lift_id]
  exact
    Equiv.Perm.fin_5_not_solvable
      (solvable_of_solvable_injective (Equiv.Perm.viaEmbeddingHom_injective (Nonempty.some key)))
#align equiv.perm.not_solvable Equiv.Perm.not_solvable

end PermNotSolvable
