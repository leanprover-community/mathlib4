/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import Mathlib.Algebra.Group.ULift
import Mathlib.Algebra.GroupWithZero.Subgroup
import Mathlib.Algebra.Module.NatInt
import Mathlib.Algebra.Order.Group.Basic
import Mathlib.GroupTheory.QuotientGroup.Defs
import Mathlib.Tactic.NormNum.Eq

/-!
# Divisible Group and rootable group

In this file, we define a divisible add monoid and a rootable monoid with some basic properties.

## Main definition

* `DivisibleBy A Œ±`: An additive monoid `A` is said to be divisible by `Œ±` iff for all `n ‚â† 0 ‚àà Œ±`
  and `y ‚àà A`, there is an `x ‚àà A` such that `n ‚Ä¢ x = y`. In this file, we adopt a constructive
  approach, i.e. we ask for an explicit `div : A ‚Üí Œ± ‚Üí A` function such that `div a 0 = 0` and
  `n ‚Ä¢ div a n = a` for all `n ‚â† 0 ‚àà Œ±`.
* `RootableBy A Œ±`: A monoid `A` is said to be rootable by `Œ±` iff for all `n ‚â† 0 ‚àà Œ±` and `y ‚àà A`,
  there is an `x ‚àà A` such that `x^n = y`. In this file, we adopt a constructive approach, i.e. we
  ask for an explicit `root : A ‚Üí Œ± ‚Üí A` function such that `root a 0 = 1` and `(root a n)‚Åø = a` for
  all `n ‚â† 0 ‚àà Œ±`.

## Main results

For additive monoids and groups:

* `divisibleByOfSMulRightSurj` : the constructive definition of divisiblity is implied by
  the condition that `n ‚Ä¢ x = a` has solutions for all `n ‚â† 0` and `a ‚àà A`.
* `smul_right_surj_of_divisibleBy` : the constructive definition of divisiblity implies
  the condition that `n ‚Ä¢ x = a` has solutions for all `n ‚â† 0` and `a ‚àà A`.
* `Prod.divisibleBy` : `A √ó B` is divisible for any two divisible additive monoids.
* `Pi.divisibleBy` : any product of divisible additive monoids is divisible.
* `AddGroup.divisibleByIntOfDivisibleByNat` : for additive groups, int divisiblity is implied
  by nat divisiblity.
* `AddGroup.divisibleByNatOfDivisibleByInt` : for additive groups, nat divisiblity is implied
  by int divisiblity.
* `AddCommGroup.divisibleByIntOfSMulTopEqTop`: the constructive definition of divisiblity
  is implied by the condition that `n ‚Ä¢ A = A` for all `n ‚â† 0`.
* `AddCommGroup.smul_top_eq_top_of_divisibleBy_int`: the constructive definition of divisiblity
  implies the condition that `n ‚Ä¢ A = A` for all `n ‚â† 0`.
* `divisibleByIntOfCharZero` : any field of characteristic zero is divisible.
* `QuotientAddGroup.divisibleBy` : quotient group of divisible group is divisible.
* `Function.Surjective.divisibleBy` : if `A` is divisible and `A ‚Üí+ B` is surjective, then `B`
  is divisible.
* `DivisibleBy.module` : a torsion-free ‚Ñï-divisible commutative group is a ‚Ñö-module.

and their multiplicative counterparts:

* `rootableByOfPowLeftSurj` : the constructive definition of rootablity is implied by the
  condition that `x‚Åø = y` has solutions for all `n ‚â† 0` and `a ‚àà A`.
* `pow_left_surj_of_rootableBy` : the constructive definition of rootablity implies the
  condition that `x‚Åø = y` has solutions for all `n ‚â† 0` and `a ‚àà A`.
* `Prod.rootableBy` : any product of two rootable monoids is rootable.
* `Pi.rootableBy` : any product of rootable monoids is rootable.
* `Group.rootableByIntOfRootableByNat` : in groups, int rootablity is implied by nat
  rootablity.
* `Group.rootableByNatOfRootableByInt` : in groups, nat rootablity is implied by int
  rootablity.
* `QuotientGroup.rootableBy` : quotient group of rootable group is rootable.
* `Function.Surjective.rootableBy` : if `A` is rootable and `A ‚Üí* B` is surjective, then `B` is
  rootable.

TODO: Show that divisibility implies injectivity in the category of `AddCommGroup`.
-/


open Pointwise

section AddMonoid

variable (A Œ± : Type*) [AddMonoid A] [SMul Œ± A] [Zero Œ±]

/--
An `AddMonoid A` is `Œ±`-divisible iff `n ‚Ä¢ x = a` has a solution for all `n ‚â† 0 ‚àà Œ±` and `a ‚àà A`.
Here we adopt a constructive approach where we ask an explicit `div : A ‚Üí Œ± ‚Üí A` function such that
* `div a 0 = 0` for all `a ‚àà A`
* `n ‚Ä¢ div a n = a` for all `n ‚â† 0 ‚àà Œ±` and `a ‚àà A`.
-/
class DivisibleBy where
  /-- The division function -/
  div : A ‚Üí Œ± ‚Üí A
  div_zero : ‚àÄ a, div a 0 = 0
  div_cancel : ‚àÄ {n : Œ±} (a : A), n ‚â† 0 ‚Üí n ‚Ä¢ div a n = a

end AddMonoid

section Monoid

variable (A Œ± : Type*) [Monoid A] [Pow A Œ±] [Zero Œ±]

/-- A `Monoid A` is `Œ±`-rootable iff `x‚Åø = a` has a solution for all `n ‚â† 0 ‚àà Œ±` and `a ‚àà A`.
Here we adopt a constructive approach where we ask an explicit `root : A ‚Üí Œ± ‚Üí A` function such that
* `root a 0 = 1` for all `a ‚àà A`
* `(root a n)‚Åø = a` for all `n ‚â† 0 ‚àà Œ±` and `a ‚àà A`.
-/
@[to_additive]
class RootableBy where
  /-- The root function -/
  root : A ‚Üí Œ± ‚Üí A
  root_zero : ‚àÄ a, root a 0 = 1
  root_cancel : ‚àÄ {n : Œ±} (a : A), n ‚â† 0 ‚Üí root a n ^ n = a

@[to_additive smul_right_surj_of_divisibleBy]
theorem pow_left_surj_of_rootableBy [RootableBy A Œ±] {n : Œ±} (hn : n ‚â† 0) :
    Function.Surjective (fun a => a ^ n : A ‚Üí A) := fun x =>
  ‚ü®RootableBy.root x n, RootableBy.root_cancel _ hn‚ü©

/--
A `Monoid A` is `Œ±`-rootable iff the `pow _ n` function is surjective, i.e. the constructive version
implies the textbook approach.
-/
@[to_additive divisibleByOfSMulRightSurj
      "An `AddMonoid A` is `Œ±`-divisible iff `n ‚Ä¢ _` is a surjective function, i.e. the constructive
      version implies the textbook approach."]
noncomputable def rootableByOfPowLeftSurj
    (H : ‚àÄ {n : Œ±}, n ‚â† 0 ‚Üí Function.Surjective (fun a => a ^ n : A ‚Üí A)) : RootableBy A Œ± where
  root a n := @dite _ (n = 0) (Classical.dec _) (fun _ => (1 : A)) fun hn => (H hn a).choose
  root_zero _ := by classical exact dif_pos rfl
  root_cancel a hn := by
    dsimp only
    rw [dif_neg hn]
    exact (H hn a).choose_spec

section Pi

variable {Œπ Œ≤ : Type*} (B : Œπ ‚Üí Type*) [‚àÄ i : Œπ, Pow (B i) Œ≤]
variable [Zero Œ≤] [‚àÄ i : Œπ, Monoid (B i)] [‚àÄ i, RootableBy (B i) Œ≤]

@[to_additive]
instance Pi.rootableBy : RootableBy (‚àÄ i, B i) Œ≤ where
  root x n i := RootableBy.root (x i) n
  root_zero _x := funext fun _i => RootableBy.root_zero _
  root_cancel _x hn := funext fun _i => RootableBy.root_cancel _ hn

end Pi

section Prod

variable {Œ≤ B B' : Type*} [Pow B Œ≤] [Pow B' Œ≤]
variable [Zero Œ≤] [Monoid B] [Monoid B'] [RootableBy B Œ≤] [RootableBy B' Œ≤]

@[to_additive]
instance Prod.rootableBy : RootableBy (B √ó B') Œ≤ where
  root p n := (RootableBy.root p.1 n, RootableBy.root p.2 n)
  root_zero _p := Prod.ext (RootableBy.root_zero _) (RootableBy.root_zero _)
  root_cancel _p hn := Prod.ext (RootableBy.root_cancel _ hn) (RootableBy.root_cancel _ hn)

end Prod

section ULift

@[to_additive]
instance ULift.instRootableBy [RootableBy A Œ±] : RootableBy (ULift A) Œ± where
  root x a := ULift.up <| RootableBy.root x.down a
  root_zero x := ULift.ext _ _ <| RootableBy.root_zero x.down
  root_cancel _ h := ULift.ext _ _ <| RootableBy.root_cancel _ h

end ULift

end Monoid

namespace AddCommGroup

variable (A : Type*) [AddCommGroup A]

theorem smul_top_eq_top_of_divisibleBy_int [DivisibleBy A ‚Ñ§] {n : ‚Ñ§} (hn : n ‚â† 0) :
    n ‚Ä¢ (‚ä§ : AddSubgroup A) = ‚ä§ :=
  AddSubgroup.map_top_of_surjective _ fun a => ‚ü®DivisibleBy.div a n, DivisibleBy.div_cancel _ hn‚ü©

/-- If for all `n ‚â† 0 ‚àà ‚Ñ§`, `n ‚Ä¢ A = A`, then `A` is divisible.
-/
noncomputable def divisibleByIntOfSMulTopEqTop
    (H : ‚àÄ {n : ‚Ñ§} (_hn : n ‚â† 0), n ‚Ä¢ (‚ä§ : AddSubgroup A) = ‚ä§) : DivisibleBy A ‚Ñ§ where
  div a n :=
    if hn : n = 0 then 0 else (show a ‚àà n ‚Ä¢ (‚ä§ : AddSubgroup A) by rw [H hn]; trivial).choose
  div_zero _ := dif_pos rfl
  div_cancel a hn := by
    simp_rw [dif_neg hn]
    generalize_proofs h1
    exact h1.choose_spec.2

end AddCommGroup

instance (priority := 100) divisibleByIntOfCharZero {ùïú} [DivisionRing ùïú] [CharZero ùïú] :
    DivisibleBy ùïú ‚Ñ§ where
  div q n := q / n
  div_zero q := by norm_num
  div_cancel {n} q hn := by
    rw [zsmul_eq_mul, (Int.cast_commute n _).eq, div_mul_cancel‚ÇÄ q (Int.cast_ne_zero.mpr hn)]

namespace Group

variable (A : Type*) [Group A]

open Int in
/-- A group is `‚Ñ§`-rootable if it is `‚Ñï`-rootable.
-/
@[to_additive "An additive group is `‚Ñ§`-divisible if it is `‚Ñï`-divisible."]
def rootableByIntOfRootableByNat [RootableBy A ‚Ñï] : RootableBy A ‚Ñ§ where
  root a z :=
    match z with
    | (n : ‚Ñï) => RootableBy.root a n
    | -[n+1] => (RootableBy.root a (n + 1))‚Åª¬π
  root_zero a := RootableBy.root_zero a
  root_cancel {n} a hn := by
    cases n
    ¬∑ rw [Int.ofNat_eq_coe, Nat.cast_ne_zero] at hn
      simp [RootableBy.root_cancel _ hn]
    ¬∑ simp [RootableBy.root_cancel _ (Nat.add_one_ne_zero _)]

/-- A group is `‚Ñï`-rootable if it is `‚Ñ§`-rootable
-/
@[to_additive "An additive group is `‚Ñï`-divisible if it `‚Ñ§`-divisible."]
def rootableByNatOfRootableByInt [RootableBy A ‚Ñ§] : RootableBy A ‚Ñï where
  root a n := RootableBy.root a (n : ‚Ñ§)
  root_zero a := RootableBy.root_zero a
  root_cancel {n} a hn := by
    -- Porting note: replaced `norm_num`
    simpa only [zpow_natCast] using RootableBy.root_cancel a (show (n : ‚Ñ§) ‚â† 0 from mod_cast hn)

end Group

section Hom

variable {A B Œ± : Type*}
variable [Zero Œ±] [Monoid A] [Monoid B] [Pow A Œ±] [Pow B Œ±] [RootableBy A Œ±]
variable (f : A ‚Üí B)

/--
If `f : A ‚Üí B` is a surjective homomorphism and `A` is `Œ±`-rootable, then `B` is also `Œ±`-rootable.
-/
@[to_additive
      "If `f : A ‚Üí B` is a surjective homomorphism and `A` is `Œ±`-divisible, then `B` is also
      `Œ±`-divisible."]
noncomputable def Function.Surjective.rootableBy (hf : Function.Surjective f)
    (hpow : ‚àÄ (a : A) (n : Œ±), f (a ^ n) = f a ^ n) : RootableBy B Œ± :=
  rootableByOfPowLeftSurj _ _ fun {n} hn x =>
    let ‚ü®y, hy‚ü© := hf x
    ‚ü®f <| RootableBy.root y n,
      (by rw [‚Üê hpow (RootableBy.root y n) n, RootableBy.root_cancel _ hn, hy] : _ ^ n = x)‚ü©

@[to_additive DivisibleBy.surjective_smul]
theorem RootableBy.surjective_pow (A Œ± : Type*) [Monoid A] [Pow A Œ±] [Zero Œ±] [RootableBy A Œ±]
    {n : Œ±} (hn : n ‚â† 0) : Function.Surjective fun a : A => a ^ n := fun a =>
  ‚ü®RootableBy.root a n, RootableBy.root_cancel a hn‚ü©

end Hom

section Quotient

variable (Œ± : Type*) {A : Type*} [CommGroup A] (B : Subgroup A)

/-- Any quotient group of a rootable group is rootable. -/
@[to_additive "Any quotient group of a divisible group is divisible"]
noncomputable instance QuotientGroup.rootableBy [RootableBy A ‚Ñï] : RootableBy (A ‚ß∏ B) ‚Ñï :=
  QuotientGroup.mk_surjective.rootableBy _ fun _ _ => rfl

end Quotient

section Nat

namespace RootableBy
variable {A : Type*} [Monoid A] [RootableBy A ‚Ñï]

@[to_additive div_one]
theorem root_one (a : A) : root a 1 = a := by
  rw [‚Üê pow_one (root a 1)]
  rw [root_cancel _ (by simp)]

@[to_additive zero_div]
theorem one_root [IsMulTorsionFree A] (n : ‚Ñï) : root (1 : A) n = 1 := by
  obtain rfl | h := eq_or_ne n 0
  ¬∑ rw [root_zero]
  ¬∑ rw [‚Üê pow_left_inj h, root_cancel _ h]
    simp

variable {A : Type*} [Group A] [RootableBy A ‚Ñï]

@[to_additive div_neg]
theorem root_inv [IsMulTorsionFree A] (a : A) (n : ‚Ñï) : root a‚Åª¬π n = (root a n)‚Åª¬π := by
  obtain rfl | h := eq_or_ne n 0
  ¬∑ rw [root_zero, root_zero]
    simp
  ¬∑ rw [‚Üê pow_left_inj h, root_cancel _ h, inv_pow, root_cancel _ h]

/-- For element `a` in a group rootable by ‚Ñï,
we define `qpow a (p / q) = root (a ^ p) q.
This is not made a `Pow` instance to avoid collision with existing instances. -/
@[to_additive (reorder := 4 5) qsmul "For element `a` in a group divisible by ‚Ñï,
we define `qsmul (p / q) a  = div (a ^ p) q.
This is not made a `SMul` instance to avoid collision with existing instances."]
def qpow (a : A) (s : ‚Ñö) := root (a ^ s.num) s.den

@[to_additive (reorder := 4 5) qsmul_eq]
theorem qpow_eq (a : A) (s : ‚Ñö) : qpow a s = root (a ^ s.num) s.den := rfl

@[to_additive one_qsmul]
theorem qpow_one (a : A) : qpow a 1 = a := by
  rw [qpow_eq, Rat.num_ofNat, zpow_one, Rat.den_ofNat, root_one]

@[to_additive zero_qsmul]
theorem qpow_zero (a : A) : qpow a 0 = 1 := by
  rw [qpow_eq, Rat.num_ofNat, zpow_zero, Rat.den_ofNat, root_one]

@[to_additive qsmul_zero]
theorem one_qpow [IsMulTorsionFree A] (s : ‚Ñö) : qpow (1 : A) s = 1 := by
  rw [qpow_eq, one_zpow, one_root]

@[to_additive (reorder := 5 6 7, 5 6 7) mul_qsmul]
theorem qpow_mul [IsMulTorsionFree A] (a : A) (x : ‚Ñö) (y : ‚Ñö) :
    qpow a (x * y) = qpow (qpow a y) x := by
  rw [qpow_eq, qpow_eq, qpow_eq]
  apply (pow_left_inj (show (x.den * y.den) ‚â† 0 by simp)).mp
  nth_rw 1 [Rat.den_mul_den_eq_den_mul_gcd]
  rw [mul_comm (x * y).den, pow_mul' (root _ _), root_cancel _ (by simp)]
  rw [mul_comm x.den, pow_mul' (root _ _), root_cancel _ (by simp)]
  rw [‚Üê zpow_natCast _ y.den]
  rw [zpow_comm _ x.num y.den, zpow_natCast _ y.den, root_cancel _ (by simp)]
  rw [‚Üê zpow_natCast (a ^ (x * y).num)]
  rw [‚Üê zpow_mul, ‚Üê zpow_mul, mul_comm y.den, mul_comm y.num]
  rw [‚Üê Rat.num_mul_num_eq_num_mul_gcd]

variable {A : Type*} [CommGroup A] [RootableBy A ‚Ñï] [IsMulTorsionFree A]

@[to_additive (reorder := 5 6 7, 5 6 7) add_qsmul]
theorem qpow_add (a : A) (x : ‚Ñö) (y : ‚Ñö) : qpow a (x + y) = qpow a x * qpow a y := by
  rw [qpow_eq, qpow_eq, qpow_eq]
  apply (pow_left_inj (show (x.den * y.den * (x + y).den) ‚â† 0 by simp)).mp
  rw [pow_mul', root_cancel _ (by simp)]
  rw [mul_comm (x.den * y.den), mul_pow]
  nth_rw 2 [mul_comm x.den y.den]
  rw [‚Üê mul_assoc, ‚Üê mul_assoc, pow_mul' _ _ x.den, pow_mul' (root (a ^ y.num) y.den) _ y.den]
  rw [root_cancel _ (by simp), root_cancel _ (by simp)]

  rw [‚Üê zpow_natCast _ (x.den * y.den)]
  rw [‚Üê zpow_natCast _ ((x + y).den * y.den)]
  rw [‚Üê zpow_natCast _ ((x + y).den * x.den)]
  push_cast

  rw [‚Üê zpow_mul, ‚Üê zpow_mul, ‚Üê zpow_mul, ‚Üê zpow_add]
  rw [mul_comm ((x + y).den : ‚Ñ§), mul_comm ((x + y).den : ‚Ñ§)]
  rw [‚Üê mul_assoc, ‚Üê mul_assoc, ‚Üê mul_assoc, ‚Üê add_mul]
  rw [Rat.add_num_den']

@[to_additive (reorder := 5 6 7) qsmul_add]
theorem mul_qpow (x : A) (y : A) (a : ‚Ñö) : qpow (x * y) a = qpow x a * qpow y a := by
  rw [qpow_eq, qpow_eq, qpow_eq]
  apply (pow_left_inj (show a.den ‚â† 0 by simp)).mp
  rw [mul_pow _ _ a.den]
  rw [root_cancel _ (by simp), root_cancel _ (by simp), root_cancel _ (by simp)]
  rw [mul_zpow]

section Order
variable {A : Type*} [CommGroup A] [RootableBy A ‚Ñï] [LinearOrder A] [IsOrderedMonoid A]

variable (A) in
@[to_additive qsmul_right_strictMono]
theorem qpow_left_strictMono {a : ‚Ñö} (ha : 0 < a) : StrictMono (qpow (A := A) ¬∑ a) := by
  intro x y hxy
  simp_rw [qpow_eq]
  apply (pow_left_strictMono a.den_ne_zero).lt_iff_lt.mp
  simp_rw [RootableBy.root_cancel _ a.den_ne_zero]
  exact (zpow_left_strictMono _ (show 0 < a.num by exact Rat.num_pos.mpr ha)).lt_iff_lt.mpr hxy

end Order

end RootableBy

namespace DivisibleBy
variable (A : Type*)

/-- Create `‚Ñö`-`MulAction` from `Divisible.qsmul`. -/
def mulAction [AddGroup A] [DivisibleBy A ‚Ñï] [IsAddTorsionFree A] : MulAction ‚Ñö A where
  smul := qsmul
  one_smul := one_qsmul
  mul_smul := mul_qsmul

/-- Create `‚Ñö`-`Module` from `Divisible.qsmul`. -/
def module [AddCommGroup A] [DivisibleBy A ‚Ñï] [IsAddTorsionFree A] : Module ‚Ñö A where
  __ := mulAction A
  smul_zero := qsmul_zero
  zero_smul := zero_qsmul
  smul_add := qsmul_add
  add_smul := add_qsmul

end DivisibleBy

end Nat
