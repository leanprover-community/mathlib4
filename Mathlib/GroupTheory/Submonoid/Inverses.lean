/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.GroupTheory.Submonoid.Pointwise

#align_import group_theory.submonoid.inverses from "leanprover-community/mathlib"@"59694bd07f0a39c5beccba34bd9f413a160782bf"

/-!

# Submonoid of inverses

Given a submonoid `N` of a monoid `M`, we define the submonoid `N.leftInv` as the submonoid of
left inverses of `N`. When `M` is commutative, we may define `fromCommLeftInv : N.leftInv ‚Üí* N`
since the inverses are unique. When `N ‚â§ IsUnit.Submonoid M`, this is precisely
the pointwise inverse of `N`, and we may define `leftInvEquiv : S.leftInv ‚âÉ* S`.

For the pointwise inverse of submonoids of groups, please refer to
`GroupTheory.Submonoid.Pointwise`.

## TODO

Define the submonoid of right inverses and two-sided inverses.
See the comments of #10679 for a possible implementation.

-/


variable {M : Type*}

namespace Submonoid

@[to_additive]
noncomputable instance [Monoid M] : Group (IsUnit.submonoid M) :=
  { inferInstanceAs (Monoid (IsUnit.submonoid M)) with
    inv := fun x ‚Ü¶ ‚ü®x.prop.unit‚Åª¬π.val, x.prop.unit‚Åª¬π.isUnit‚ü©
    mul_left_inv := fun x ‚Ü¶
      Subtype.ext ((Units.val_mul x.prop.unit‚Åª¬π _).trans x.prop.unit.inv_val) }

@[to_additive]
noncomputable instance [CommMonoid M] : CommGroup (IsUnit.submonoid M) :=
  { inferInstanceAs (Group (IsUnit.submonoid M)) with
    mul_comm := fun a b ‚Ü¶ by convert mul_comm a b }
                             -- üéâ no goals

@[to_additive]
theorem IsUnit.Submonoid.coe_inv [Monoid M] (x : IsUnit.submonoid M) :
    ‚Üëx‚Åª¬π = (‚Üëx.prop.unit‚Åª¬π : M) :=
  rfl
#align submonoid.is_unit.submonoid.coe_inv Submonoid.IsUnit.Submonoid.coe_inv
#align add_submonoid.is_unit.submonoid.coe_neg AddSubmonoid.IsUnit.Submonoid.coe_neg

section Monoid

variable [Monoid M] (S : Submonoid M)

/-- `S.leftInv` is the submonoid containing all the left inverses of `S`. -/
@[to_additive
      "`S.leftNeg` is the additive submonoid containing all the left additive inverses of `S`."]
def leftInv : Submonoid M where
  carrier := { x : M | ‚àÉ y : S, x * y = 1 }
  one_mem' := ‚ü®1, mul_one 1‚ü©
  mul_mem' := fun {a} _b ‚ü®a', ha‚ü© ‚ü®b', hb‚ü© ‚Ü¶
    ‚ü®b' * a', by simp only [coe_mul, ‚Üê mul_assoc, mul_assoc a, hb, mul_one, ha]‚ü©
                 -- üéâ no goals
#align submonoid.left_inv Submonoid.leftInv
#align add_submonoid.left_neg AddSubmonoid.leftNeg

@[to_additive]
theorem leftInv_leftInv_le : S.leftInv.leftInv ‚â§ S := by
  rintro x ‚ü®‚ü®y, z, h‚ÇÅ‚ü©, h‚ÇÇ : x * y = 1‚ü©
  -- ‚ä¢ x ‚àà S
  convert z.prop
  -- ‚ä¢ x = ‚Üëz
  rw [‚Üê mul_one x, ‚Üê h‚ÇÅ, ‚Üê mul_assoc, h‚ÇÇ, one_mul]
  -- üéâ no goals
#align submonoid.left_inv_left_inv_le Submonoid.leftInv_leftInv_le
#align add_submonoid.left_neg_left_neg_le AddSubmonoid.leftNeg_leftNeg_le

@[to_additive]
theorem unit_mem_leftInv (x : MÀ£) (hx : (x : M) ‚àà S) : ((x‚Åª¬π : _) : M) ‚àà S.leftInv :=
  ‚ü®‚ü®x, hx‚ü©, x.inv_val‚ü©
#align submonoid.unit_mem_left_inv Submonoid.unit_mem_leftInv
#align add_submonoid.add_unit_mem_left_neg AddSubmonoid.addUnit_mem_leftNeg

@[to_additive]
theorem leftInv_leftInv_eq (hS : S ‚â§ IsUnit.submonoid M) : S.leftInv.leftInv = S := by
  refine' le_antisymm S.leftInv_leftInv_le _
  -- ‚ä¢ S ‚â§ leftInv (leftInv S)
  intro x hx
  -- ‚ä¢ x ‚àà leftInv (leftInv S)
  have : x = ((hS hx).unit‚Åª¬π‚Åª¬π : MÀ£) := by
    rw [inv_inv (hS hx).unit]
    rfl
  rw [this]
  -- ‚ä¢ ‚Üë(IsUnit.unit (_ : x ‚àà IsUnit.submonoid M))‚Åª¬π‚Åª¬π ‚àà leftInv (leftInv S)
  exact S.leftInv.unit_mem_leftInv _ (S.unit_mem_leftInv _ hx)
  -- üéâ no goals
#align submonoid.left_inv_left_inv_eq Submonoid.leftInv_leftInv_eq
#align add_submonoid.left_neg_left_neg_eq AddSubmonoid.leftNeg_leftNeg_eq

/-- The function from `S.leftInv` to `S` sending an element to its right inverse in `S`.
This is a `MonoidHom` when `M` is commutative. -/
@[to_additive
      "The function from `S.leftAdd` to `S` sending an element to its right additive
inverse in `S`. This is an `AddMonoidHom` when `M` is commutative."]
noncomputable def fromLeftInv : S.leftInv ‚Üí S := fun x ‚Ü¶ x.prop.choose
#align submonoid.from_left_inv Submonoid.fromLeftInv
#align add_submonoid.from_left_neg AddSubmonoid.fromLeftNeg

@[to_additive (attr := simp)]
theorem mul_fromLeftInv (x : S.leftInv) : (x : M) * S.fromLeftInv x = 1 :=
  x.prop.choose_spec
#align submonoid.mul_from_left_inv Submonoid.mul_fromLeftInv
#align add_submonoid.add_from_left_neg AddSubmonoid.add_fromLeftNeg

@[to_additive (attr := simp)]
theorem fromLeftInv_one : S.fromLeftInv 1 = 1 :=
  (one_mul _).symm.trans (Subtype.eq <| S.mul_fromLeftInv 1)
#align submonoid.from_left_inv_one Submonoid.fromLeftInv_one
#align add_submonoid.from_left_neg_zero AddSubmonoid.fromLeftNeg_zero

end Monoid

section CommMonoid

variable [CommMonoid M] (S : Submonoid M)

@[to_additive (attr := simp)]
theorem fromLeftInv_mul (x : S.leftInv) : (S.fromLeftInv x : M) * x = 1 := by
  rw [mul_comm, mul_fromLeftInv]
  -- üéâ no goals
#align submonoid.from_left_inv_mul Submonoid.fromLeftInv_mul
#align add_submonoid.from_left_neg_add AddSubmonoid.fromLeftNeg_add

@[to_additive]
theorem leftInv_le_isUnit : S.leftInv ‚â§ IsUnit.submonoid M := fun x ‚ü®y, hx‚ü© ‚Ü¶
  ‚ü®‚ü®x, y, hx, mul_comm x y ‚ñ∏ hx‚ü©, rfl‚ü©
#align submonoid.left_inv_le_is_unit Submonoid.leftInv_le_isUnit
#align add_submonoid.left_neg_le_is_add_unit AddSubmonoid.leftNeg_le_isAddUnit

@[to_additive]
theorem fromLeftInv_eq_iff (a : S.leftInv) (b : M) : (S.fromLeftInv a : M) = b ‚Üî (a : M) * b = 1 :=
  by rw [‚Üê IsUnit.mul_right_inj (leftInv_le_isUnit _ a.prop), S.mul_fromLeftInv, eq_comm]
     -- üéâ no goals
#align submonoid.from_left_inv_eq_iff Submonoid.fromLeftInv_eq_iff
#align add_submonoid.from_left_neg_eq_iff AddSubmonoid.fromLeftNeg_eq_iff

/-- The `MonoidHom` from `S.leftInv` to `S` sending an element to its right inverse in `S`. -/
@[to_additive (attr := simps)
    "The `AddMonoidHom` from `S.leftNeg` to `S` sending an element to its
    right additive inverse in `S`."]
noncomputable def fromCommLeftInv : S.leftInv ‚Üí* S where
  toFun := S.fromLeftInv
  map_one' := S.fromLeftInv_one
  map_mul' x y :=
    Subtype.ext <| by
      rw [fromLeftInv_eq_iff, mul_comm x, Submonoid.coe_mul, Submonoid.coe_mul, mul_assoc, ‚Üê
        mul_assoc (x : M), mul_fromLeftInv, one_mul, mul_fromLeftInv]
#align submonoid.from_comm_left_inv Submonoid.fromCommLeftInv
#align add_submonoid.from_comm_left_neg AddSubmonoid.fromCommLeftNeg

variable (hS : S ‚â§ IsUnit.submonoid M)

-- Porting note: commented out next line
 -- include hS

/-- The submonoid of pointwise inverse of `S` is `MulEquiv` to `S`. -/
@[to_additive (attr := simps apply) "The additive submonoid of pointwise additive inverse of `S` is
`AddEquiv` to `S`."]
noncomputable def leftInvEquiv : S.leftInv ‚âÉ* S :=
  { S.fromCommLeftInv with
    invFun := fun x ‚Ü¶ by
      choose x' hx using hS x.prop
      -- ‚ä¢ { x // x ‚àà leftInv S }
      exact ‚ü®x'.inv, x, hx ‚ñ∏ x'.inv_val‚ü©
      -- üéâ no goals
    left_inv := fun x ‚Ü¶
      Subtype.eq <| by
        dsimp only; generalize_proofs h; rw [‚Üê h.choose.mul_left_inj]
        -- ‚ä¢ (Classical.choose (_ : ‚Üë(OneHom.toFun (‚Üë(fromCommLeftInv S)) x) ‚àà IsUnit.sub ‚Ä¶
                    -- ‚ä¢ (Classical.choose h).inv = ‚Üëx
                                         -- ‚ä¢ (Classical.choose h).inv * ‚Üë(Exists.choose h) = ‚Üëx * ‚Üë(Exists.choose h)
        conv => rhs; rw [h.choose_spec]
        -- ‚ä¢ (Classical.choose h).inv * ‚Üë(Exists.choose h) = ‚Üëx * ‚Üë(OneHom.toFun (‚Üë(fromC ‚Ä¶
        exact h.choose.inv_val.trans (S.mul_fromLeftInv x).symm
        -- üéâ no goals
    right_inv := fun x ‚Ü¶ by
      dsimp only [fromCommLeftInv]
      -- ‚ä¢ fromLeftInv S { val := (Classical.choose (_ : ‚Üëx ‚àà IsUnit.submonoid M)).inv, ‚Ä¶
      ext
      -- ‚ä¢ ‚Üë(fromLeftInv S { val := (Classical.choose (_ : ‚Üëx ‚àà IsUnit.submonoid M)).in ‚Ä¶
      rw [fromLeftInv_eq_iff]
      -- ‚ä¢ ‚Üë{ val := (Classical.choose (_ : ‚Üëx ‚àà IsUnit.submonoid M)).inv, property :=  ‚Ä¶
      convert (hS x.prop).choose.inv_val
      -- ‚ä¢ ‚Üëx = ‚Üë(Exists.choose (_ : ‚Üëx ‚àà IsUnit.submonoid M))
      exact (hS x.prop).choose_spec.symm }
      -- üéâ no goals
#align submonoid.left_inv_equiv Submonoid.leftInvEquiv
#align add_submonoid.left_neg_equiv AddSubmonoid.leftNegEquiv

@[to_additive (attr := simp)]
theorem fromLeftInv_leftInvEquiv_symm (x : S) : S.fromLeftInv ((S.leftInvEquiv hS).symm x) = x :=
  (S.leftInvEquiv hS).right_inv x
#align submonoid.from_left_inv_left_inv_equiv_symm Submonoid.fromLeftInv_leftInvEquiv_symm
#align add_submonoid.from_left_neg_left_neg_equiv_symm AddSubmonoid.fromLeftNeg_leftNegEquiv_symm

@[to_additive (attr := simp)]
theorem leftInvEquiv_symm_fromLeftInv (x : S.leftInv) :
    (S.leftInvEquiv hS).symm (S.fromLeftInv x) = x :=
  (S.leftInvEquiv hS).left_inv x
#align submonoid.left_inv_equiv_symm_from_left_inv Submonoid.leftInvEquiv_symm_fromLeftInv
#align add_submonoid.left_neg_equiv_symm_from_left_neg AddSubmonoid.leftNegEquiv_symm_fromLeftNeg

@[to_additive]
theorem leftInvEquiv_mul (x : S.leftInv) : (S.leftInvEquiv hS x : M) * x = 1 := by
  simpa only [leftInvEquiv_apply, fromCommLeftInv] using fromLeftInv_mul S x
  -- üéâ no goals
#align submonoid.left_inv_equiv_mul Submonoid.leftInvEquiv_mul
#align add_submonoid.left_neg_equiv_add AddSubmonoid.leftNegEquiv_add

@[to_additive]
theorem mul_leftInvEquiv (x : S.leftInv) : (x : M) * S.leftInvEquiv hS x = 1 := by
  simp only [leftInvEquiv_apply, fromCommLeftInv, mul_fromLeftInv]
  -- üéâ no goals
#align submonoid.mul_left_inv_equiv Submonoid.mul_leftInvEquiv
#align add_submonoid.add_left_neg_equiv AddSubmonoid.add_leftNegEquiv

@[to_additive (attr := simp)]
theorem leftInvEquiv_symm_mul (x : S) : ((S.leftInvEquiv hS).symm x : M) * x = 1 := by
  convert S.mul_leftInvEquiv hS ((S.leftInvEquiv hS).symm x)
  -- ‚ä¢ x = ‚Üë(leftInvEquiv S hS) (‚Üë(MulEquiv.symm (leftInvEquiv S hS)) x)
  simp
  -- üéâ no goals
#align submonoid.left_inv_equiv_symm_mul Submonoid.leftInvEquiv_symm_mul
#align add_submonoid.left_neg_equiv_symm_add AddSubmonoid.leftNegEquiv_symm_add

@[to_additive (attr := simp)]
theorem mul_leftInvEquiv_symm (x : S) : (x : M) * (S.leftInvEquiv hS).symm x = 1 := by
  convert S.leftInvEquiv_mul hS ((S.leftInvEquiv hS).symm x)
  -- ‚ä¢ x = ‚Üë(leftInvEquiv S hS) (‚Üë(MulEquiv.symm (leftInvEquiv S hS)) x)
  simp
  -- üéâ no goals
#align submonoid.mul_left_inv_equiv_symm Submonoid.mul_leftInvEquiv_symm
#align add_submonoid.add_left_neg_equiv_symm AddSubmonoid.add_leftNegEquiv_symm

end CommMonoid

section Group

variable [Group M] (S : Submonoid M)

open Pointwise

@[to_additive]
theorem leftInv_eq_inv : S.leftInv = S‚Åª¬π :=
  Submonoid.ext fun _ ‚Ü¶
    ‚ü®fun h ‚Ü¶ Submonoid.mem_inv.mpr ((inv_eq_of_mul_eq_one_right h.choose_spec).symm ‚ñ∏
      h.choose.prop),
      fun h ‚Ü¶ ‚ü®‚ü®_, h‚ü©, mul_right_inv _‚ü©‚ü©
#align submonoid.left_inv_eq_inv Submonoid.leftInv_eq_inv
#align add_submonoid.left_neg_eq_neg AddSubmonoid.leftNeg_eq_neg

@[to_additive (attr := simp)]
theorem fromLeftInv_eq_inv (x : S.leftInv) : (S.fromLeftInv x : M) = (x : M)‚Åª¬π := by
  rw [‚Üê mul_right_inj (x : M), mul_right_inv, mul_fromLeftInv]
  -- üéâ no goals
#align submonoid.from_left_inv_eq_inv Submonoid.fromLeftInv_eq_inv
#align add_submonoid.from_left_neg_eq_neg AddSubmonoid.fromLeftNeg_eq_neg

end Group

section CommGroup

variable [CommGroup M] (S : Submonoid M) (hS : S ‚â§ IsUnit.submonoid M)

@[to_additive (attr := simp)]
theorem leftInvEquiv_symm_eq_inv (x : S) : ((S.leftInvEquiv hS).symm x : M) = (x : M)‚Åª¬π := by
  rw [‚Üê mul_right_inj (x : M), mul_right_inv, mul_leftInvEquiv_symm]
  -- üéâ no goals
#align submonoid.left_inv_equiv_symm_eq_inv Submonoid.leftInvEquiv_symm_eq_inv
#align add_submonoid.left_neg_equiv_symm_eq_neg AddSubmonoid.leftNegEquiv_symm_eq_neg

end CommGroup

end Submonoid
