/-
Copyright (c) 2021 Riccardo Brasca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Riccardo Brasca
-/
import Mathlib.Data.Set.Pointwise.Finite
import Mathlib.GroupTheory.QuotientGroup
import Mathlib.GroupTheory.Submonoid.Operations
import Mathlib.GroupTheory.Subgroup.Basic
import Mathlib.SetTheory.Cardinal.Finite
import Mathlib.Data.Finset.Preimage

#align_import group_theory.finiteness from "leanprover-community/mathlib"@"dde670c9a3f503647fd5bfdf1037bad526d3397a"

/-!
# Finitely generated monoids and groups

We define finitely generated monoids and groups. See also `Submodule.FG` and `Module.Finite` for
finitely-generated modules.

## Main definition

* `Submonoid.FG S`, `AddSubmonoid.FG S` : A submonoid `S` is finitely generated.
* `Monoid.FG M`, `AddMonoid.FG M` : A typeclass indicating a type `M` is finitely generated as a
monoid.
* `Subgroup.FG S`, `AddSubgroup.FG S` : A subgroup `S` is finitely generated.
* `Group.FG M`, `AddGroup.FG M` : A typeclass indicating a type `M` is finitely generated as a
group.

-/


/-! ### Monoids and submonoids -/


open Pointwise

variable {M N : Type*} [Monoid M] [AddMonoid N]

section Submonoid

/-- A submonoid of `M` is finitely generated if it is the closure of a finite subset of `M`. -/
@[to_additive]
def Submonoid.FG (P : Submonoid M) : Prop :=
  ‚àÉ S : Finset M, Submonoid.closure ‚ÜëS = P
#align submonoid.fg Submonoid.FG
#align add_submonoid.fg AddSubmonoid.FG

/-- An additive submonoid of `N` is finitely generated if it is the closure of a finite subset of
`M`. -/
add_decl_doc AddSubmonoid.FG

/-- An equivalent expression of `Submonoid.FG` in terms of `Set.Finite` instead of `Finset`. -/
@[to_additive "An equivalent expression of `AddSubmonoid.FG` in terms of `Set.Finite` instead of
`Finset`."]
theorem Submonoid.fg_iff (P : Submonoid M) :
    Submonoid.FG P ‚Üî ‚àÉ S : Set M, Submonoid.closure S = P ‚àß S.Finite :=
  ‚ü®fun ‚ü®S, hS‚ü© => ‚ü®S, hS, Finset.finite_toSet S‚ü©, fun ‚ü®S, hS, hf‚ü© =>
    ‚ü®Set.Finite.toFinset hf, by simp [hS]‚ü©‚ü©
                                -- üéâ no goals
#align submonoid.fg_iff Submonoid.fg_iff
#align add_submonoid.fg_iff AddSubmonoid.fg_iff

theorem Submonoid.fg_iff_add_fg (P : Submonoid M) : P.FG ‚Üî P.toAddSubmonoid.FG :=
  ‚ü®fun h =>
    let ‚ü®S, hS, hf‚ü© := (Submonoid.fg_iff _).1 h
    (AddSubmonoid.fg_iff _).mpr
      ‚ü®Additive.toMul ‚Åª¬π' S, by simp [‚Üê Submonoid.toAddSubmonoid_closure, hS], hf‚ü©,
                                -- üéâ no goals
    fun h =>
    let ‚ü®T, hT, hf‚ü© := (AddSubmonoid.fg_iff _).1 h
    (Submonoid.fg_iff _).mpr
      ‚ü®Multiplicative.ofAdd ‚Åª¬π' T, by simp [‚Üê AddSubmonoid.toSubmonoid'_closure, hT], hf‚ü©‚ü©
                                      -- üéâ no goals
#align submonoid.fg_iff_add_fg Submonoid.fg_iff_add_fg

theorem AddSubmonoid.fg_iff_mul_fg (P : AddSubmonoid N) : P.FG ‚Üî P.toSubmonoid.FG := by
  convert (Submonoid.fg_iff_add_fg (toSubmonoid P)).symm
  -- üéâ no goals
#align add_submonoid.fg_iff_mul_fg AddSubmonoid.fg_iff_mul_fg

end Submonoid

section Monoid

variable (M N)

/-- A monoid is finitely generated if it is finitely generated as a submonoid of itself. -/
class Monoid.FG : Prop where
  out : (‚ä§ : Submonoid M).FG
#align monoid.fg Monoid.FG

/-- An additive monoid is finitely generated if it is finitely generated as an additive submonoid of
itself. -/
class AddMonoid.FG : Prop where
  out : (‚ä§ : AddSubmonoid N).FG
#align add_monoid.fg AddMonoid.FG

attribute [to_additive] Monoid.FG

variable {M N}

theorem Monoid.fg_def : Monoid.FG M ‚Üî (‚ä§ : Submonoid M).FG :=
  ‚ü®fun h => h.1, fun h => ‚ü®h‚ü©‚ü©
#align monoid.fg_def Monoid.fg_def

theorem AddMonoid.fg_def : AddMonoid.FG N ‚Üî (‚ä§ : AddSubmonoid N).FG :=
  ‚ü®fun h => h.1, fun h => ‚ü®h‚ü©‚ü©
#align add_monoid.fg_def AddMonoid.fg_def

/-- An equivalent expression of `Monoid.FG` in terms of `Set.Finite` instead of `Finset`. -/
@[to_additive
      "An equivalent expression of `AddMonoid.FG` in terms of `Set.Finite` instead of `Finset`."]
theorem Monoid.fg_iff :
    Monoid.FG M ‚Üî ‚àÉ S : Set M, Submonoid.closure S = (‚ä§ : Submonoid M) ‚àß S.Finite :=
  ‚ü®fun h => (Submonoid.fg_iff ‚ä§).1 h.out, fun h => ‚ü®(Submonoid.fg_iff ‚ä§).2 h‚ü©‚ü©
#align monoid.fg_iff Monoid.fg_iff
#align add_monoid.fg_iff AddMonoid.fg_iff

theorem Monoid.fg_iff_add_fg : Monoid.FG M ‚Üî AddMonoid.FG (Additive M) :=
  ‚ü®fun h => ‚ü®(Submonoid.fg_iff_add_fg ‚ä§).1 h.out‚ü©, fun h => ‚ü®(Submonoid.fg_iff_add_fg ‚ä§).2 h.out‚ü©‚ü©
#align monoid.fg_iff_add_fg Monoid.fg_iff_add_fg

theorem AddMonoid.fg_iff_mul_fg : AddMonoid.FG N ‚Üî Monoid.FG (Multiplicative N) :=
  ‚ü®fun h => ‚ü®(AddSubmonoid.fg_iff_mul_fg ‚ä§).1 h.out‚ü©, fun h =>
    ‚ü®(AddSubmonoid.fg_iff_mul_fg ‚ä§).2 h.out‚ü©‚ü©
#align add_monoid.fg_iff_mul_fg AddMonoid.fg_iff_mul_fg

instance AddMonoid.fg_of_monoid_fg [Monoid.FG M] : AddMonoid.FG (Additive M) :=
  Monoid.fg_iff_add_fg.1 ‚Äπ_‚Ä∫
#align add_monoid.fg_of_monoid_fg AddMonoid.fg_of_monoid_fg

instance Monoid.fg_of_addMonoid_fg [AddMonoid.FG N] : Monoid.FG (Multiplicative N) :=
  AddMonoid.fg_iff_mul_fg.1 ‚Äπ_‚Ä∫
#align monoid.fg_of_add_monoid_fg Monoid.fg_of_addMonoid_fg

@[to_additive]
instance (priority := 100) Monoid.fg_of_finite [Finite M] : Monoid.FG M := by
  cases nonempty_fintype M
  -- ‚ä¢ FG M
  exact ‚ü®‚ü®Finset.univ, by rw [Finset.coe_univ]; exact Submonoid.closure_univ‚ü©‚ü©
  -- üéâ no goals
#align monoid.fg_of_finite Monoid.fg_of_finite
#align add_monoid.fg_of_finite AddMonoid.fg_of_finite

end Monoid

@[to_additive]
theorem Submonoid.FG.map {M' : Type*} [Monoid M'] {P : Submonoid M} (h : P.FG) (e : M ‚Üí* M') :
    (P.map e).FG := by
  classical
    obtain ‚ü®s, rfl‚ü© := h
    exact ‚ü®s.image e, by rw [Finset.coe_image, MonoidHom.map_mclosure]‚ü©
#align submonoid.fg.map Submonoid.FG.map
#align add_submonoid.fg.map AddSubmonoid.FG.map

@[to_additive]
theorem Submonoid.FG.map_injective {M' : Type*} [Monoid M'] {P : Submonoid M} (e : M ‚Üí* M')
    (he : Function.Injective e) (h : (P.map e).FG) : P.FG := by
  obtain ‚ü®s, hs‚ü© := h
  -- ‚ä¢ FG P
  use s.preimage e (he.injOn _)
  -- ‚ä¢ closure ‚Üë(Finset.preimage s ‚Üëe (_ : Set.InjOn (‚Üëe) (‚Üëe ‚Åª¬π' ‚Üës))) = P
  apply Submonoid.map_injective_of_injective he
  -- ‚ä¢ Submonoid.map e (closure ‚Üë(Finset.preimage s ‚Üëe (_ : Set.InjOn (‚Üëe) (‚Üëe ‚Åª¬π'  ‚Ä¶
  rw [‚Üê hs, MonoidHom.map_mclosure e, Finset.coe_preimage]
  -- ‚ä¢ closure (‚Üëe '' (‚Üëe ‚Åª¬π' ‚Üës)) = closure ‚Üës
  congr
  -- ‚ä¢ ‚Üëe '' (‚Üëe ‚Åª¬π' ‚Üës) = ‚Üës
  rw [Set.image_preimage_eq_iff, ‚Üê MonoidHom.coe_mrange e, ‚Üê Submonoid.closure_le, hs,
      MonoidHom.mrange_eq_map e]
  exact Submonoid.monotone_map le_top
  -- üéâ no goals
#align submonoid.fg.map_injective Submonoid.FG.map_injective
#align add_submonoid.fg.map_injective AddSubmonoid.FG.map_injective

@[to_additive (attr := simp)]
theorem Monoid.fg_iff_submonoid_fg (N : Submonoid M) : Monoid.FG N ‚Üî N.FG := by
  conv_rhs => rw [‚Üê N.range_subtype, MonoidHom.mrange_eq_map]
  -- ‚ä¢ FG { x // x ‚àà N } ‚Üî Submonoid.FG (Submonoid.map (Submonoid.subtype N) ‚ä§)
  exact ‚ü®fun h => h.out.map N.subtype, fun h => ‚ü®h.map_injective N.subtype Subtype.coe_injective‚ü©‚ü©
  -- üéâ no goals
#align monoid.fg_iff_submonoid_fg Monoid.fg_iff_submonoid_fg
#align add_monoid.fg_iff_add_submonoid_fg AddMonoid.fg_iff_addSubmonoid_fg

@[to_additive]
theorem Monoid.fg_of_surjective {M' : Type*} [Monoid M'] [Monoid.FG M] (f : M ‚Üí* M')
    (hf : Function.Surjective f) : Monoid.FG M' := by
  classical
    obtain ‚ü®s, hs‚ü© := Monoid.fg_def.mp ‚Äπ_‚Ä∫
    use s.image f
    rwa [Finset.coe_image, ‚Üê MonoidHom.map_mclosure, hs, ‚Üê MonoidHom.mrange_eq_map,
      MonoidHom.mrange_top_iff_surjective]
#align monoid.fg_of_surjective Monoid.fg_of_surjective
#align add_monoid.fg_of_surjective AddMonoid.fg_of_surjective

@[to_additive]
instance Monoid.fg_range {M' : Type*} [Monoid M'] [Monoid.FG M] (f : M ‚Üí* M') :
    Monoid.FG (MonoidHom.mrange f) :=
  Monoid.fg_of_surjective f.mrangeRestrict f.mrangeRestrict_surjective
#align monoid.fg_range Monoid.fg_range
#align add_monoid.fg_range AddMonoid.fg_range

@[to_additive AddSubmonoid.multiples_fg]
theorem Submonoid.powers_fg (r : M) : (Submonoid.powers r).FG :=
  ‚ü®{r}, (Finset.coe_singleton r).symm ‚ñ∏ (Submonoid.powers_eq_closure r).symm‚ü©
#align submonoid.powers_fg Submonoid.powers_fg
#align add_submonoid.multiples_fg AddSubmonoid.multiples_fg

@[to_additive AddMonoid.multiples_fg]
instance Monoid.powers_fg (r : M) : Monoid.FG (Submonoid.powers r) :=
  (Monoid.fg_iff_submonoid_fg _).mpr (Submonoid.powers_fg r)
#align monoid.powers_fg Monoid.powers_fg
#align add_monoid.multiples_fg AddMonoid.multiples_fg

@[to_additive]
instance Monoid.closure_finset_fg (s : Finset M) : Monoid.FG (Submonoid.closure (s : Set M)) := by
  refine' ‚ü®‚ü®s.preimage Subtype.val (Subtype.coe_injective.injOn _), _‚ü©‚ü©
  -- ‚ä¢ Submonoid.closure ‚Üë(Finset.preimage s Subtype.val (_ : Set.InjOn (fun a => ‚Üë ‚Ä¶
  rw [Finset.coe_preimage, Submonoid.closure_closure_coe_preimage]
  -- üéâ no goals
#align monoid.closure_finset_fg Monoid.closure_finset_fg
#align add_monoid.closure_finset_fg AddMonoid.closure_finset_fg

@[to_additive]
instance Monoid.closure_finite_fg (s : Set M) [Finite s] : Monoid.FG (Submonoid.closure s) :=
  haveI := Fintype.ofFinite s
  s.coe_toFinset ‚ñ∏ Monoid.closure_finset_fg s.toFinset
#align monoid.closure_finite_fg Monoid.closure_finite_fg
#align add_monoid.closure_finite_fg AddMonoid.closure_finite_fg

/-! ### Groups and subgroups -/


variable {G H : Type*} [Group G] [AddGroup H]

section Subgroup

/-- A subgroup of `G` is finitely generated if it is the closure of a finite subset of `G`. -/
@[to_additive]
def Subgroup.FG (P : Subgroup G) : Prop :=
  ‚àÉ S : Finset G, Subgroup.closure ‚ÜëS = P
#align subgroup.fg Subgroup.FG
#align add_subgroup.fg AddSubgroup.FG

/-- An additive subgroup of `H` is finitely generated if it is the closure of a finite subset of
`H`. -/
add_decl_doc AddSubgroup.FG

/-- An equivalent expression of `Subgroup.FG` in terms of `Set.Finite` instead of `Finset`. -/
@[to_additive "An equivalent expression of `AddSubgroup.fg` in terms of `Set.Finite` instead of
`Finset`."]
theorem Subgroup.fg_iff (P : Subgroup G) :
    Subgroup.FG P ‚Üî ‚àÉ S : Set G, Subgroup.closure S = P ‚àß S.Finite :=
  ‚ü®fun ‚ü®S, hS‚ü© => ‚ü®S, hS, Finset.finite_toSet S‚ü©, fun ‚ü®S, hS, hf‚ü© =>
    ‚ü®Set.Finite.toFinset hf, by simp [hS]‚ü©‚ü©
                                -- üéâ no goals
#align subgroup.fg_iff Subgroup.fg_iff
#align add_subgroup.fg_iff AddSubgroup.fg_iff

/-- A subgroup is finitely generated if and only if it is finitely generated as a submonoid. -/
@[to_additive "An additive subgroup is finitely generated if
and only if it is finitely generated as an additive submonoid."]
theorem Subgroup.fg_iff_submonoid_fg (P : Subgroup G) : P.FG ‚Üî P.toSubmonoid.FG := by
  constructor
  -- ‚ä¢ FG P ‚Üí Submonoid.FG P.toSubmonoid
  ¬∑ rintro ‚ü®S, rfl‚ü©
    -- ‚ä¢ Submonoid.FG (closure ‚ÜëS).toSubmonoid
    rw [Submonoid.fg_iff]
    -- ‚ä¢ ‚àÉ S_1, Submonoid.closure S_1 = (closure ‚ÜëS).toSubmonoid ‚àß Set.Finite S_1
    refine' ‚ü®S ‚à™ S‚Åª¬π, _, S.finite_toSet.union S.finite_toSet.inv‚ü©
    -- ‚ä¢ Submonoid.closure (‚ÜëS ‚à™ (‚ÜëS)‚Åª¬π) = (closure ‚ÜëS).toSubmonoid
    exact (Subgroup.closure_toSubmonoid _).symm
    -- üéâ no goals
  ¬∑ rintro ‚ü®S, hS‚ü©
    -- ‚ä¢ FG P
    refine' ‚ü®S, le_antisymm _ _‚ü©
    -- ‚ä¢ closure ‚ÜëS ‚â§ P
    ¬∑ rw [Subgroup.closure_le, ‚Üê Subgroup.coe_toSubmonoid, ‚Üê hS]
      -- ‚ä¢ ‚ÜëS ‚äÜ ‚Üë(Submonoid.closure ‚ÜëS)
      exact Submonoid.subset_closure
      -- üéâ no goals
    ¬∑ rw [‚Üê Subgroup.toSubmonoid_le, ‚Üê hS, Submonoid.closure_le]
      -- ‚ä¢ ‚ÜëS ‚äÜ ‚Üë(closure ‚ÜëS).toSubmonoid
      exact Subgroup.subset_closure
      -- üéâ no goals
#align subgroup.fg_iff_submonoid_fg Subgroup.fg_iff_submonoid_fg
#align add_subgroup.fg_iff_add_submonoid.fg AddSubgroup.fg_iff_addSubmonoid_fg

theorem Subgroup.fg_iff_add_fg (P : Subgroup G) : P.FG ‚Üî P.toAddSubgroup.FG := by
  rw [Subgroup.fg_iff_submonoid_fg, AddSubgroup.fg_iff_addSubmonoid_fg]
  -- ‚ä¢ Submonoid.FG P.toSubmonoid ‚Üî AddSubmonoid.FG (‚ÜëtoAddSubgroup P).toAddSubmonoid
  exact (Subgroup.toSubmonoid P).fg_iff_add_fg
  -- üéâ no goals
#align subgroup.fg_iff_add_fg Subgroup.fg_iff_add_fg

theorem AddSubgroup.fg_iff_mul_fg (P : AddSubgroup H) : P.FG ‚Üî P.toSubgroup.FG := by
  rw [AddSubgroup.fg_iff_addSubmonoid_fg, Subgroup.fg_iff_submonoid_fg]
  -- ‚ä¢ AddSubmonoid.FG P.toAddSubmonoid ‚Üî Submonoid.FG (‚ÜëtoSubgroup P).toSubmonoid
  exact AddSubmonoid.fg_iff_mul_fg (AddSubgroup.toAddSubmonoid P)
  -- üéâ no goals
#align add_subgroup.fg_iff_mul_fg AddSubgroup.fg_iff_mul_fg

end Subgroup

section Group

variable (G H)

/-- A group is finitely generated if it is finitely generated as a submonoid of itself. -/
class Group.FG : Prop where
  out : (‚ä§ : Subgroup G).FG
#align group.fg Group.FG

/-- An additive group is finitely generated if it is finitely generated as an additive submonoid of
itself. -/
class AddGroup.FG : Prop where
  out : (‚ä§ : AddSubgroup H).FG
#align add_group.fg AddGroup.FG

attribute [to_additive] Group.FG

variable {G H}

theorem Group.fg_def : Group.FG G ‚Üî (‚ä§ : Subgroup G).FG :=
  ‚ü®fun h => h.1, fun h => ‚ü®h‚ü©‚ü©
#align group.fg_def Group.fg_def

theorem AddGroup.fg_def : AddGroup.FG H ‚Üî (‚ä§ : AddSubgroup H).FG :=
  ‚ü®fun h => h.1, fun h => ‚ü®h‚ü©‚ü©
#align add_group.fg_def AddGroup.fg_def

/-- An equivalent expression of `Group.FG` in terms of `Set.Finite` instead of `Finset`. -/
@[to_additive
      "An equivalent expression of `AddGroup.fg` in terms of `Set.Finite` instead of `Finset`."]
theorem Group.fg_iff : Group.FG G ‚Üî ‚àÉ S : Set G, Subgroup.closure S = (‚ä§ : Subgroup G) ‚àß S.Finite :=
  ‚ü®fun h => (Subgroup.fg_iff ‚ä§).1 h.out, fun h => ‚ü®(Subgroup.fg_iff ‚ä§).2 h‚ü©‚ü©
#align group.fg_iff Group.fg_iff
#align add_group.fg_iff AddGroup.fg_iff

@[to_additive]
theorem Group.fg_iff' :
    Group.FG G ‚Üî ‚àÉ (n : _) (S : Finset G), S.card = n ‚àß Subgroup.closure (S : Set G) = ‚ä§ :=
  Group.fg_def.trans ‚ü®fun ‚ü®S, hS‚ü© => ‚ü®S.card, S, rfl, hS‚ü©, fun ‚ü®_n, S, _hn, hS‚ü© => ‚ü®S, hS‚ü©‚ü©
#align group.fg_iff' Group.fg_iff'
#align add_group.fg_iff' AddGroup.fg_iff'

/-- A group is finitely generated if and only if it is finitely generated as a monoid. -/
@[to_additive "An additive group is finitely generated if and only
if it is finitely generated as an additive monoid."]
theorem Group.fg_iff_monoid_fg : Group.FG G ‚Üî Monoid.FG G :=
  ‚ü®fun h => Monoid.fg_def.2 <| (Subgroup.fg_iff_submonoid_fg ‚ä§).1 (Group.fg_def.1 h), fun h =>
    Group.fg_def.2 <| (Subgroup.fg_iff_submonoid_fg ‚ä§).2 (Monoid.fg_def.1 h)‚ü©
#align group.fg_iff_monoid.fg Group.fg_iff_monoid_fg
#align add_group.fg_iff_add_monoid.fg AddGroup.fg_iff_addMonoid_fg

@[to_additive (attr := simp)]
theorem Group.fg_iff_subgroup_fg (H : Subgroup G) : Group.FG H ‚Üî H.FG :=
  (fg_iff_monoid_fg.trans (Monoid.fg_iff_submonoid_fg _)).trans
    (Subgroup.fg_iff_submonoid_fg _).symm

theorem GroupFG.iff_add_fg : Group.FG G ‚Üî AddGroup.FG (Additive G) :=
  ‚ü®fun h => ‚ü®(Subgroup.fg_iff_add_fg ‚ä§).1 h.out‚ü©, fun h => ‚ü®(Subgroup.fg_iff_add_fg ‚ä§).2 h.out‚ü©‚ü©
#align group_fg.iff_add_fg GroupFG.iff_add_fg

theorem AddGroup.fg_iff_mul_fg : AddGroup.FG H ‚Üî Group.FG (Multiplicative H) :=
  ‚ü®fun h => ‚ü®(AddSubgroup.fg_iff_mul_fg ‚ä§).1 h.out‚ü©, fun h =>
    ‚ü®(AddSubgroup.fg_iff_mul_fg ‚ä§).2 h.out‚ü©‚ü©
#align add_group.fg_iff_mul_fg AddGroup.fg_iff_mul_fg

instance AddGroup.fg_of_group_fg [Group.FG G] : AddGroup.FG (Additive G) :=
  GroupFG.iff_add_fg.1 ‚Äπ_‚Ä∫
#align add_group.fg_of_group_fg AddGroup.fg_of_group_fg

instance Group.fg_of_mul_group_fg [AddGroup.FG H] : Group.FG (Multiplicative H) :=
  AddGroup.fg_iff_mul_fg.1 ‚Äπ_‚Ä∫
#align group.fg_of_mul_group_fg Group.fg_of_mul_group_fg

@[to_additive]
instance (priority := 100) Group.fg_of_finite [Finite G] : Group.FG G := by
  cases nonempty_fintype G
  -- ‚ä¢ FG G
  exact ‚ü®‚ü®Finset.univ, by rw [Finset.coe_univ]; exact Subgroup.closure_univ‚ü©‚ü©
  -- üéâ no goals
#align group.fg_of_finite Group.fg_of_finite
#align add_group.fg_of_finite AddGroup.fg_of_finite

@[to_additive]
theorem Group.fg_of_surjective {G' : Type*} [Group G'] [hG : Group.FG G] {f : G ‚Üí* G'}
    (hf : Function.Surjective f) : Group.FG G' :=
  Group.fg_iff_monoid_fg.mpr <|
    @Monoid.fg_of_surjective G _ G' _ (Group.fg_iff_monoid_fg.mp hG) f hf
#align group.fg_of_surjective Group.fg_of_surjective
#align add_group.fg_of_surjective AddGroup.fg_of_surjective

@[to_additive]
instance Group.fg_range {G' : Type*} [Group G'] [Group.FG G] (f : G ‚Üí* G') : Group.FG f.range :=
  Group.fg_of_surjective f.rangeRestrict_surjective
#align group.fg_range Group.fg_range
#align add_group.fg_range AddGroup.fg_range

@[to_additive]
instance Group.closure_finset_fg (s : Finset G) : Group.FG (Subgroup.closure (s : Set G)) := by
  refine' ‚ü®‚ü®s.preimage Subtype.val (Subtype.coe_injective.injOn _), _‚ü©‚ü©
  -- ‚ä¢ Subgroup.closure ‚Üë(Finset.preimage s Subtype.val (_ : Set.InjOn (fun a => ‚Üëa ‚Ä¶
  rw [Finset.coe_preimage, ‚Üê Subgroup.coeSubtype, Subgroup.closure_preimage_eq_top]
  -- üéâ no goals
#align group.closure_finset_fg Group.closure_finset_fg
#align add_group.closure_finset_fg AddGroup.closure_finset_fg

@[to_additive]
instance Group.closure_finite_fg (s : Set G) [Finite s] : Group.FG (Subgroup.closure s) :=
  haveI := Fintype.ofFinite s
  s.coe_toFinset ‚ñ∏ Group.closure_finset_fg s.toFinset
#align group.closure_finite_fg Group.closure_finite_fg
#align add_group.closure_finite_fg AddGroup.closure_finite_fg

variable (G)

/-- The minimum number of generators of a group. -/
@[to_additive "The minimum number of generators of an additive group"]
noncomputable def Group.rank [h : Group.FG G] :=
  @Nat.find _ (Classical.decPred _) (Group.fg_iff'.mp h)
#align group.rank Group.rank
#align add_group.rank AddGroup.rank

@[to_additive]
theorem Group.rank_spec [h : Group.FG G] :
    ‚àÉ S : Finset G, S.card = Group.rank G ‚àß Subgroup.closure (S : Set G) = ‚ä§ :=
  @Nat.find_spec _ (Classical.decPred _) (Group.fg_iff'.mp h)
#align group.rank_spec Group.rank_spec
#align add_group.rank_spec AddGroup.rank_spec

@[to_additive]
theorem Group.rank_le [h : Group.FG G] {S : Finset G} (hS : Subgroup.closure (S : Set G) = ‚ä§) :
    Group.rank G ‚â§ S.card :=
  @Nat.find_le _ _ (Classical.decPred _) (Group.fg_iff'.mp h) ‚ü®S, rfl, hS‚ü©
#align group.rank_le Group.rank_le
#align add_group.rank_le AddGroup.rank_le

variable {G} {G' : Type*} [Group G']

@[to_additive]
theorem Group.rank_le_of_surjective [Group.FG G] [Group.FG G'] (f : G ‚Üí* G')
    (hf : Function.Surjective f) : Group.rank G' ‚â§ Group.rank G := by
  classical
    obtain ‚ü®S, hS1, hS2‚ü© := Group.rank_spec G
    trans (S.image f).card
    ¬∑ apply Group.rank_le
      rw [Finset.coe_image, ‚Üê MonoidHom.map_closure, hS2, Subgroup.map_top_of_surjective f hf]
    ¬∑ exact Finset.card_image_le.trans_eq hS1
#align group.rank_le_of_surjective Group.rank_le_of_surjective
#align add_group.rank_le_of_surjective AddGroup.rank_le_of_surjective

@[to_additive]
theorem Group.rank_range_le [Group.FG G] {f : G ‚Üí* G'} : Group.rank f.range ‚â§ Group.rank G :=
  Group.rank_le_of_surjective f.rangeRestrict f.rangeRestrict_surjective
#align group.rank_range_le Group.rank_range_le
#align add_group.rank_range_le AddGroup.rank_range_le

@[to_additive]
theorem Group.rank_congr [Group.FG G] [Group.FG G'] (f : G ‚âÉ* G') : Group.rank G = Group.rank G' :=
  le_antisymm (Group.rank_le_of_surjective f.symm f.symm.surjective)
    (Group.rank_le_of_surjective f f.surjective)
#align group.rank_congr Group.rank_congr
#align add_group.rank_congr AddGroup.rank_congr

end Group

namespace Subgroup

@[to_additive]
theorem rank_congr {H K : Subgroup G} [Group.FG H] [Group.FG K] (h : H = K) :
    Group.rank H = Group.rank K := by subst h; rfl
                                      -- ‚ä¢ Group.rank { x // x ‚àà H } = Group.rank { x // x ‚àà H }
                                               -- üéâ no goals
#align subgroup.rank_congr Subgroup.rank_congr
#align add_subgroup.rank_congr AddSubgroup.rank_congr

@[to_additive]
theorem rank_closure_finset_le_card (s : Finset G) : Group.rank (closure (s : Set G)) ‚â§ s.card := by
  classical
    let t : Finset (closure (s : Set G)) := s.preimage Subtype.val (Subtype.coe_injective.injOn _)
    have ht : closure (t : Set (closure (s : Set G))) = ‚ä§ := by
      rw [Finset.coe_preimage]
      exact closure_preimage_eq_top (s : Set G)
    apply (Group.rank_le (closure (s : Set G)) ht).trans
    suffices H : Set.InjOn Subtype.val (t : Set (closure (s : Set G)))
    rw [‚Üê Finset.card_image_of_injOn H, Finset.image_preimage]
    ¬∑ apply Finset.card_filter_le
    ¬∑ apply Subtype.coe_injective.injOn
#align subgroup.rank_closure_finset_le_card Subgroup.rank_closure_finset_le_card
#align add_subgroup.rank_closure_finset_le_card AddSubgroup.rank_closure_finset_le_card

@[to_additive]
theorem rank_closure_finite_le_nat_card (s : Set G) [Finite s] :
    Group.rank (closure s) ‚â§ Nat.card s := by
  haveI := Fintype.ofFinite s
  -- ‚ä¢ Group.rank { x // x ‚àà closure s } ‚â§ Nat.card ‚Üës
  rw [Nat.card_eq_fintype_card, ‚Üê s.toFinset_card, ‚Üê rank_congr (congr_arg _ s.coe_toFinset)]
  -- ‚ä¢ Group.rank { x // x ‚àà closure ‚Üë(Set.toFinset s) } ‚â§ Finset.card (Set.toFinse ‚Ä¶
  exact rank_closure_finset_le_card s.toFinset
  -- üéâ no goals
#align subgroup.rank_closure_finite_le_nat_card Subgroup.rank_closure_finite_le_nat_card
#align add_subgroup.rank_closure_finite_le_nat_card AddSubgroup.rank_closure_finite_le_nat_card

end Subgroup

section QuotientGroup

@[to_additive]
instance QuotientGroup.fg [Group.FG G] (N : Subgroup G) [Subgroup.Normal N] : Group.FG <| G ‚ß∏ N :=
  Group.fg_of_surjective <| QuotientGroup.mk'_surjective N
#align quotient_group.fg QuotientGroup.fg
#align quotient_add_group.fg QuotientAddGroup.fg

end QuotientGroup
