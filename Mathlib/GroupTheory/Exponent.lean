/-
Copyright (c) 2021 Julian Kuelshammer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Julian Kuelshammer
-/
import Mathlib.Data.ZMod.Quotient
import Mathlib.GroupTheory.NoncommPiCoprod
import Mathlib.GroupTheory.OrderOfElement
import Mathlib.Algebra.GCDMonoid.Finset
import Mathlib.Data.Nat.Factorization.Basic
import Mathlib.Tactic.ByContra

#align_import group_theory.exponent from "leanprover-community/mathlib"@"52fa514ec337dd970d71d8de8d0fd68b455a1e54"

/-!
# Exponent of a group

This file defines the exponent of a group, or more generally a monoid. For a group `G` it is defined
to be the minimal `n‚â•1` such that `g ^ n = 1` for all `g ‚àà G`. For a finite group `G`,
it is equal to the lowest common multiple of the order of all elements of the group `G`.

## Main definitions

* `Monoid.ExponentExists` is a predicate on a monoid `G` saying that there is some positive `n`
  such that `g ^ n = 1` for all `g ‚àà G`.
* `Monoid.exponent` defines the exponent of a monoid `G` as the minimal positive `n` such that
  `g ^ n = 1` for all `g ‚àà G`, by convention it is `0` if no such `n` exists.
* `AddMonoid.ExponentExists` the additive version of `Monoid.ExponentExists`.
* `AddMonoid.exponent` the additive version of `Monoid.exponent`.

## Main results

* `Monoid.lcm_order_eq_exponent`: For a finite left cancel monoid `G`, the exponent is equal to the
  `Finset.lcm` of the order of its elements.
* `Monoid.exponent_eq_iSup_orderOf(')`: For a commutative cancel monoid, the exponent is
  equal to `‚®Ü g : G, orderOf g` (or zero if it has any order-zero elements).

## TODO
* Refactor the characteristic of a ring to be the exponent of its underlying additive group.
-/


universe u

variable {G : Type u}

open Classical

namespace Monoid

section Monoid

variable (G) [Monoid G]

/-- A predicate on a monoid saying that there is a positive integer `n` such that `g ^ n = 1`
  for all `g`.-/
@[to_additive
      "A predicate on an additive monoid saying that there is a positive integer `n` such\n
      that `n ‚Ä¢ g = 0` for all `g`."]
def ExponentExists :=
  ‚àÉ n, 0 < n ‚àß ‚àÄ g : G, g ^ n = 1
#align monoid.exponent_exists Monoid.ExponentExists
#align add_monoid.exponent_exists AddMonoid.ExponentExists

/-- The exponent of a group is the smallest positive integer `n` such that `g ^ n = 1` for all
  `g ‚àà G` if it exists, otherwise it is zero by convention.-/
@[to_additive
      "The exponent of an additive group is the smallest positive integer `n` such that\n
      `n ‚Ä¢ g = 0` for all `g ‚àà G` if it exists, otherwise it is zero by convention."]
noncomputable def exponent :=
  if h : ExponentExists G then Nat.find h else 0
#align monoid.exponent Monoid.exponent
#align add_monoid.exponent AddMonoid.exponent

variable {G}

@[to_additive]
theorem exponentExists_iff_ne_zero : ExponentExists G ‚Üî exponent G ‚â† 0 := by
  rw [exponent]
  -- ‚ä¢ ExponentExists G ‚Üî (if h : ExponentExists G then Nat.find h else 0) ‚â† 0
  split_ifs with h
  -- ‚ä¢ ExponentExists G ‚Üî Nat.find h ‚â† 0
  ¬∑ simp [h, @not_lt_zero' ‚Ñï]
    -- üéâ no goals
  --if this isn't done this way, `to_additive` freaks
  ¬∑ tauto
    -- üéâ no goals
#align monoid.exponent_exists_iff_ne_zero Monoid.exponentExists_iff_ne_zero
#align add_monoid.exponent_exists_iff_ne_zero AddMonoid.exponentExists_iff_ne_zero

@[to_additive]
theorem exponent_eq_zero_iff : exponent G = 0 ‚Üî ¬¨ExponentExists G := by
  simp only [exponentExists_iff_ne_zero, Classical.not_not]
  -- üéâ no goals
#align monoid.exponent_eq_zero_iff Monoid.exponent_eq_zero_iff
#align add_monoid.exponent_eq_zero_iff AddMonoid.exponent_eq_zero_iff

@[to_additive exponent_eq_zero_addOrder_zero]
theorem exponent_eq_zero_of_order_zero {g : G} (hg : orderOf g = 0) : exponent G = 0 :=
  exponent_eq_zero_iff.mpr fun ‚ü®n, hn, hgn‚ü© => orderOf_eq_zero_iff'.mp hg n hn <| hgn g
#align monoid.exponent_eq_zero_of_order_zero Monoid.exponent_eq_zero_of_order_zero
#align add_monoid.exponent_eq_zero_of_order_zero AddMonoid.exponent_eq_zero_addOrder_zero

@[to_additive exponent_nsmul_eq_zero]
theorem pow_exponent_eq_one (g : G) : g ^ exponent G = 1 := by
  by_cases ExponentExists G
  -- ‚ä¢ g ^ exponent G = 1
  -- ‚ä¢ g ^ exponent G = 1
  ¬∑ simp_rw [exponent, dif_pos h]
    -- ‚ä¢ g ^ Nat.find h = 1
    exact (Nat.find_spec h).2 g
    -- üéâ no goals
  ¬∑ simp_rw [exponent, dif_neg h, pow_zero]
    -- üéâ no goals
#align monoid.pow_exponent_eq_one Monoid.pow_exponent_eq_one
#align add_monoid.exponent_nsmul_eq_zero AddMonoid.exponent_nsmul_eq_zero

@[to_additive]
theorem pow_eq_mod_exponent {n : ‚Ñï} (g : G) : g ^ n = g ^ (n % exponent G) :=
  calc
    g ^ n = g ^ (n % exponent G + exponent G * (n / exponent G)) := by rw [Nat.mod_add_div]
                                                                       -- üéâ no goals
    _ = g ^ (n % exponent G) := by simp [pow_add, pow_mul, pow_exponent_eq_one]
                                   -- üéâ no goals

#align monoid.pow_eq_mod_exponent Monoid.pow_eq_mod_exponent
#align add_monoid.nsmul_eq_mod_exponent AddMonoid.nsmul_eq_mod_exponent

@[to_additive]
theorem exponent_pos_of_exists (n : ‚Ñï) (hpos : 0 < n) (hG : ‚àÄ g : G, g ^ n = 1) :
    0 < exponent G := by
  have h : ‚àÉ n, 0 < n ‚àß ‚àÄ g : G, g ^ n = 1 := ‚ü®n, hpos, hG‚ü©
  -- ‚ä¢ 0 < exponent G
  rw [exponent, dif_pos]
  -- ‚ä¢ 0 < Nat.find ?hc
  exact (Nat.find_spec h).1
  -- üéâ no goals
#align monoid.exponent_pos_of_exists Monoid.exponent_pos_of_exists
#align add_monoid.exponent_pos_of_exists AddMonoid.exponent_pos_of_exists

@[to_additive]
theorem exponent_min' (n : ‚Ñï) (hpos : 0 < n) (hG : ‚àÄ g : G, g ^ n = 1) : exponent G ‚â§ n := by
  rw [exponent, dif_pos]
  -- ‚ä¢ Nat.find ?hc ‚â§ n
  ¬∑ apply Nat.find_min'
    -- ‚ä¢ 0 < n ‚àß ‚àÄ (g : G), g ^ n = 1
    exact ‚ü®hpos, hG‚ü©
    -- üéâ no goals
  ¬∑ exact ‚ü®n, hpos, hG‚ü©
    -- üéâ no goals
#align monoid.exponent_min' Monoid.exponent_min'
#align add_monoid.exponent_min' AddMonoid.exponent_min'

@[to_additive]
theorem exponent_min (m : ‚Ñï) (hpos : 0 < m) (hm : m < exponent G) : ‚àÉ g : G, g ^ m ‚â† 1 := by
  by_contra' h
  -- ‚ä¢ False
  have hcon : exponent G ‚â§ m := exponent_min' m hpos h
  -- ‚ä¢ False
  linarith
  -- üéâ no goals
#align monoid.exponent_min Monoid.exponent_min
#align add_monoid.exponent_min AddMonoid.exponent_min

@[to_additive (attr := simp)]
theorem exp_eq_one_of_subsingleton [Subsingleton G] : exponent G = 1 := by
  apply le_antisymm
  -- ‚ä¢ exponent G ‚â§ 1
  ¬∑ apply exponent_min' _ Nat.one_pos
    -- ‚ä¢ ‚àÄ (g : G), g ^ 1 = 1
    simp
    -- üéâ no goals
  ¬∑ apply Nat.succ_le_of_lt
    -- ‚ä¢ 0 < exponent G
    apply exponent_pos_of_exists 1 Nat.one_pos
    -- ‚ä¢ ‚àÄ (g : G), g ^ 1 = 1
    simp
    -- üéâ no goals
#align monoid.exp_eq_one_of_subsingleton Monoid.exp_eq_one_of_subsingleton
#align add_monoid.exp_eq_zero_of_subsingleton AddMonoid.exp_eq_zero_of_subsingleton

@[to_additive addOrder_dvd_exponent]
theorem order_dvd_exponent (g : G) : orderOf g ‚à£ exponent G :=
  orderOf_dvd_of_pow_eq_one <| pow_exponent_eq_one g
#align monoid.order_dvd_exponent Monoid.order_dvd_exponent
#align add_monoid.add_order_dvd_exponent AddMonoid.addOrder_dvd_exponent

variable (G)

@[to_additive]
theorem exponent_dvd_of_forall_pow_eq_one (G) [Monoid G] (n : ‚Ñï) (hG : ‚àÄ g : G, g ^ n = 1) :
    exponent G ‚à£ n := by
  rcases n.eq_zero_or_pos with (rfl | hpos)
  -- ‚ä¢ exponent G ‚à£ 0
  ¬∑ exact dvd_zero _
    -- üéâ no goals
  apply Nat.dvd_of_mod_eq_zero
  -- ‚ä¢ n % exponent G = 0
  by_contra h
  -- ‚ä¢ False
  have h‚ÇÅ := Nat.pos_of_ne_zero h
  -- ‚ä¢ False
  have h‚ÇÇ : n % exponent G < exponent G := Nat.mod_lt _ (exponent_pos_of_exists n hpos hG)
  -- ‚ä¢ False
  have h‚ÇÉ : exponent G ‚â§ n % exponent G := by
    apply exponent_min' _ h‚ÇÅ
    simp_rw [‚Üê pow_eq_mod_exponent]
    exact hG
  linarith
  -- üéâ no goals
#align monoid.exponent_dvd_of_forall_pow_eq_one Monoid.exponent_dvd_of_forall_pow_eq_one
#align add_monoid.exponent_dvd_of_forall_nsmul_eq_zero AddMonoid.exponent_dvd_of_forall_nsmul_eq_zero

@[to_additive]
theorem lcm_orderOf_dvd_exponent [Fintype G] :
    (Finset.univ : Finset G).lcm orderOf ‚à£ exponent G := by
  apply Finset.lcm_dvd
  -- ‚ä¢ ‚àÄ (b : G), b ‚àà Finset.univ ‚Üí orderOf b ‚à£ exponent G
  intro g _
  -- ‚ä¢ orderOf g ‚à£ exponent G
  exact order_dvd_exponent g
  -- üéâ no goals
#align monoid.lcm_order_of_dvd_exponent Monoid.lcm_orderOf_dvd_exponent
#align add_monoid.lcm_add_order_of_dvd_exponent AddMonoid.lcm_addOrderOf_dvd_exponent

@[to_additive exists_addOrderOf_eq_pow_padic_val_nat_add_exponent]
theorem _root_.Nat.Prime.exists_orderOf_eq_pow_factorization_exponent {p : ‚Ñï} (hp : p.Prime) :
    ‚àÉ g : G, orderOf g = p ^ (exponent G).factorization p := by
  haveI := Fact.mk hp
  -- ‚ä¢ ‚àÉ g, orderOf g = p ^ ‚Üë(Nat.factorization (exponent G)) p
  rcases eq_or_ne ((exponent G).factorization p) 0 with (h | h)
  -- ‚ä¢ ‚àÉ g, orderOf g = p ^ ‚Üë(Nat.factorization (exponent G)) p
  ¬∑ refine' ‚ü®1, by rw [h, pow_zero, orderOf_one]‚ü©
    -- üéâ no goals
  have he : 0 < exponent G :=
    Ne.bot_lt fun ht => by
      rw [ht] at h
      apply h
      rw [bot_eq_zero, Nat.factorization_zero, Finsupp.zero_apply]
  rw [‚Üê Finsupp.mem_support_iff] at h
  -- ‚ä¢ ‚àÉ g, orderOf g = p ^ ‚Üë(Nat.factorization (exponent G)) p
  obtain ‚ü®g, hg‚ü© : ‚àÉ g : G, g ^ (exponent G / p) ‚â† 1 := by
    suffices key : ¬¨exponent G ‚à£ exponent G / p
    ¬∑ simpa using mt (exponent_dvd_of_forall_pow_eq_one G (exponent G / p)) key
    exact fun hd =>
      hp.one_lt.not_le
        ((mul_le_iff_le_one_left he).mp <|
          Nat.le_of_dvd he <| Nat.mul_dvd_of_dvd_div (Nat.dvd_of_mem_factorization h) hd)
  obtain ‚ü®k, hk : exponent G = p ^ _ * k‚ü© := Nat.ord_proj_dvd _ _
  -- ‚ä¢ ‚àÉ g, orderOf g = p ^ ‚Üë(Nat.factorization (exponent G)) p
  obtain ‚ü®t, ht‚ü© := Nat.exists_eq_succ_of_ne_zero (Finsupp.mem_support_iff.mp h)
  -- ‚ä¢ ‚àÉ g, orderOf g = p ^ ‚Üë(Nat.factorization (exponent G)) p
  refine' ‚ü®g ^ k, _‚ü©
  -- ‚ä¢ orderOf (g ^ k) = p ^ ‚Üë(Nat.factorization (exponent G)) p
  rw [ht]
  -- ‚ä¢ orderOf (g ^ k) = p ^ Nat.succ t
  apply orderOf_eq_prime_pow
  -- ‚ä¢ ¬¨(g ^ k) ^ p ^ t = 1
  ¬∑ rwa [hk, mul_comm, ht, pow_succ', ‚Üê mul_assoc, Nat.mul_div_cancel _ hp.pos, pow_mul] at hg
    -- üéâ no goals
  ¬∑ rw [‚Üê Nat.succ_eq_add_one, ‚Üê ht, ‚Üê pow_mul, mul_comm, ‚Üê hk]
    -- ‚ä¢ g ^ exponent G = 1
    exact pow_exponent_eq_one g
    -- üéâ no goals
#align nat.prime.exists_order_of_eq_pow_factorization_exponent Nat.Prime.exists_orderOf_eq_pow_factorization_exponent
#align nat.prime.exists_order_of_eq_pow_padic_val_nat_add_exponent Nat.Prime.exists_addOrderOf_eq_pow_padic_val_nat_add_exponent

variable {G}

@[to_additive]
theorem exponent_ne_zero_iff_range_orderOf_finite (h : ‚àÄ g : G, 0 < orderOf g) :
    exponent G ‚â† 0 ‚Üî (Set.range (orderOf : G ‚Üí ‚Ñï)).Finite := by
  refine' ‚ü®fun he => _, fun he => _‚ü©
  -- ‚ä¢ Set.Finite (Set.range orderOf)
  ¬∑ by_contra h
    -- ‚ä¢ False
    obtain ‚ü®m, ‚ü®t, rfl‚ü©, het‚ü© := Set.Infinite.exists_gt h (exponent G)
    -- ‚ä¢ False
    exact pow_ne_one_of_lt_orderOf' he het (pow_exponent_eq_one t)
    -- üéâ no goals
  ¬∑ lift Set.range (orderOf (G := G)) to Finset ‚Ñï using he with t ht
    -- ‚ä¢ exponent G ‚â† 0
    have htpos : 0 < t.prod id := by
      refine' Finset.prod_pos fun a ha => _
      rw [‚Üê Finset.mem_coe, ht] at ha
      obtain ‚ü®k, rfl‚ü© := ha
      exact h k
    suffices exponent G ‚à£ t.prod id by
      intro h
      rw [h, zero_dvd_iff] at this
      exact htpos.ne' this
    refine' exponent_dvd_of_forall_pow_eq_one _ _ fun g => _
    -- ‚ä¢ g ^ Finset.prod t id = 1
    rw [pow_eq_mod_orderOf, Nat.mod_eq_zero_of_dvd, pow_zero g]
    -- ‚ä¢ orderOf g ‚à£ Finset.prod t id
    apply Finset.dvd_prod_of_mem
    -- ‚ä¢ Function.minimalPeriod (fun x => g * x) 1 ‚àà t
    rw [‚Üê Finset.mem_coe, ht]
    -- ‚ä¢ Function.minimalPeriod (fun x => g * x) 1 ‚àà Set.range orderOf
    exact Set.mem_range_self g
    -- üéâ no goals
#align monoid.exponent_ne_zero_iff_range_order_of_finite Monoid.exponent_ne_zero_iff_range_orderOf_finite
#align add_monoid.exponent_ne_zero_iff_range_order_of_finite AddMonoid.exponent_ne_zero_iff_range_addOrderOf_finite

@[to_additive]
theorem exponent_eq_zero_iff_range_orderOf_infinite (h : ‚àÄ g : G, 0 < orderOf g) :
    exponent G = 0 ‚Üî (Set.range (orderOf : G ‚Üí ‚Ñï)).Infinite := by
  have := exponent_ne_zero_iff_range_orderOf_finite h
  -- ‚ä¢ exponent G = 0 ‚Üî Set.Infinite (Set.range orderOf)
  rwa [Ne.def, not_iff_comm, Iff.comm] at this
  -- üéâ no goals
#align monoid.exponent_eq_zero_iff_range_order_of_infinite Monoid.exponent_eq_zero_iff_range_orderOf_infinite
#align add_monoid.exponent_eq_zero_iff_range_order_of_infinite AddMonoid.exponent_eq_zero_iff_range_addOrderOf_infinite

@[to_additive lcm_addOrder_eq_exponent]
theorem lcm_order_eq_exponent [Fintype G] : (Finset.univ : Finset G).lcm orderOf = exponent G := by
  apply Nat.dvd_antisymm (lcm_orderOf_dvd_exponent G)
  -- ‚ä¢ exponent G ‚à£ Finset.lcm Finset.univ orderOf
  refine' exponent_dvd_of_forall_pow_eq_one G _ fun g => _
  -- ‚ä¢ g ^ Finset.lcm Finset.univ orderOf = 1
  obtain ‚ü®m, hm‚ü© : orderOf g ‚à£ Finset.univ.lcm orderOf := Finset.dvd_lcm (Finset.mem_univ g)
  -- ‚ä¢ g ^ Finset.lcm Finset.univ orderOf = 1
  rw [hm, pow_mul, pow_orderOf_eq_one, one_pow]
  -- üéâ no goals
#align monoid.lcm_order_eq_exponent Monoid.lcm_order_eq_exponent
#align add_monoid.lcm_add_order_eq_exponent AddMonoid.lcm_addOrder_eq_exponent

end Monoid

section LeftCancelMonoid

variable [LeftCancelMonoid G]

@[to_additive]
theorem exponent_ne_zero_of_finite [Finite G] : exponent G ‚â† 0 := by
  cases nonempty_fintype G
  -- ‚ä¢ exponent G ‚â† 0
  simpa [‚Üê lcm_order_eq_exponent, Finset.lcm_eq_zero_iff] using fun x => (orderOf_pos x).ne'
  -- üéâ no goals
#align monoid.exponent_ne_zero_of_finite Monoid.exponent_ne_zero_of_finite
#align add_monoid.exponent_ne_zero_of_finite AddMonoid.exponent_ne_zero_of_finite

end LeftCancelMonoid

section CommMonoid

variable [CommMonoid G]

@[to_additive]
theorem exponent_eq_iSup_orderOf (h : ‚àÄ g : G, 0 < orderOf g) :
    exponent G = ‚®Ü g : G, orderOf g := by
  rw [iSup]
  -- ‚ä¢ exponent G = sSup (Set.range fun g => orderOf g)
  rcases eq_or_ne (exponent G) 0 with (he | he)
  -- ‚ä¢ exponent G = sSup (Set.range fun g => orderOf g)
  ¬∑ rw [he, Set.Infinite.Nat.sSup_eq_zero <| (exponent_eq_zero_iff_range_orderOf_infinite h).1 he]
    -- üéâ no goals
  have hne : (Set.range (orderOf : G ‚Üí ‚Ñï)).Nonempty := ‚ü®1, 1, orderOf_one‚ü©
  -- ‚ä¢ exponent G = sSup (Set.range fun g => orderOf g)
  have hfin : (Set.range (orderOf : G ‚Üí ‚Ñï)).Finite := by
    rwa [‚Üê exponent_ne_zero_iff_range_orderOf_finite h]
  obtain ‚ü®t, ht‚ü© := hne.cSup_mem hfin
  -- ‚ä¢ exponent G = sSup (Set.range fun g => orderOf g)
  apply Nat.dvd_antisymm _
  -- ‚ä¢ sSup (Set.range fun g => orderOf g) ‚à£ exponent G
  ¬∑ rw [‚Üê ht]
    -- ‚ä¢ orderOf t ‚à£ exponent G
    apply order_dvd_exponent
    -- üéâ no goals
  refine' Nat.dvd_of_factors_subperm he _
  -- ‚ä¢ Nat.factors (exponent G) <+~ Nat.factors (sSup (Set.range fun g => orderOf g))
  rw [List.subperm_ext_iff]
  -- ‚ä¢ ‚àÄ (x : ‚Ñï), x ‚àà Nat.factors (exponent G) ‚Üí List.count x (Nat.factors (exponen ‚Ä¶
  by_contra' h
  -- ‚ä¢ False
  obtain ‚ü®p, hp, hpe‚ü© := h
  -- ‚ä¢ False
  replace hp := Nat.prime_of_mem_factors hp
  -- ‚ä¢ False
  simp only [Nat.factors_count_eq] at hpe
  -- ‚ä¢ False
  set k := (orderOf t).factorization p with hk
  -- ‚ä¢ False
  obtain ‚ü®g, hg‚ü© := hp.exists_orderOf_eq_pow_factorization_exponent G
  -- ‚ä¢ False
  suffices orderOf t < orderOf (t ^ p ^ k * g) by
    rw [ht] at this
    exact this.not_le (le_csSup hfin.bddAbove <| Set.mem_range_self _)
  have hpk : p ^ k ‚à£ orderOf t := Nat.ord_proj_dvd _ _
  -- ‚ä¢ orderOf t < orderOf (t ^ p ^ k * g)
  have hpk' : orderOf (t ^ p ^ k) = orderOf t / p ^ k := by
    rw [orderOf_pow' t (pow_ne_zero k hp.ne_zero), Nat.gcd_eq_right hpk]
  obtain ‚ü®a, ha‚ü© := Nat.exists_eq_add_of_lt hpe
  -- ‚ä¢ orderOf t < orderOf (t ^ p ^ k * g)
  have hcoprime : (orderOf (t ^ p ^ k)).coprime (orderOf g) := by
    rw [hg, Nat.coprime_pow_right_iff (pos_of_gt hpe), Nat.coprime_comm]
    apply Or.resolve_right (Nat.coprime_or_dvd_of_prime hp _)
    nth_rw 1 [‚Üê pow_one p]
    have : 1 = (Nat.factorization (orderOf (t ^ p ^ k))) p + 1 := by
     rw [hpk', Nat.factorization_div hpk]
     simp [hp]
    rw [this]
    -- Porting note: convert made to_additive complain
    apply Nat.pow_succ_factorization_not_dvd (h <| t ^ p ^ k).ne' hp
  rw [(Commute.all _ g).orderOf_mul_eq_mul_orderOf_of_coprime hcoprime, hpk',
    hg, ha, ‚Üê ht, ‚Üê hk, pow_add, pow_add, pow_one, ‚Üê mul_assoc, ‚Üê mul_assoc,
    Nat.div_mul_cancel, mul_assoc, lt_mul_iff_one_lt_right <| h t, ‚Üê pow_succ']
  exact one_lt_pow hp.one_lt a.succ_ne_zero
  -- ‚ä¢ p ^ k ‚à£ orderOf t
  exact hpk
  -- üéâ no goals
#align monoid.exponent_eq_supr_order_of Monoid.exponent_eq_iSup_orderOf
#align add_monoid.exponent_eq_supr_order_of AddMonoid.exponent_eq_iSup_addOrderOf

@[to_additive]
theorem exponent_eq_iSup_orderOf' :
    exponent G = if ‚àÉ g : G, orderOf g = 0 then 0 else ‚®Ü g : G, orderOf g := by
  split_ifs with h
  -- ‚ä¢ exponent G = 0
  ¬∑ obtain ‚ü®g, hg‚ü© := h
    -- ‚ä¢ exponent G = 0
    exact exponent_eq_zero_of_order_zero hg
    -- üéâ no goals
  ¬∑ have := not_exists.mp h
    -- ‚ä¢ exponent G = ‚®Ü (g : G), orderOf g
    exact exponent_eq_iSup_orderOf fun g => Ne.bot_lt <| this g
    -- üéâ no goals
#align monoid.exponent_eq_supr_order_of' Monoid.exponent_eq_iSup_orderOf'
#align add_monoid.exponent_eq_supr_order_of' AddMonoid.exponent_eq_iSup_addOrderOf'

end CommMonoid

section CancelCommMonoid

variable [CancelCommMonoid G]

@[to_additive]
theorem exponent_eq_max'_orderOf [Fintype G] :
    exponent G = ((@Finset.univ G _).image orderOf).max' ‚ü®1, by simp‚ü© := by
                                                                -- üéâ no goals
  rw [‚Üê Finset.Nonempty.cSup_eq_max', Finset.coe_image, Finset.coe_univ, Set.image_univ, ‚Üê iSup]
  -- ‚ä¢ exponent G = iSup orderOf
  exact exponent_eq_iSup_orderOf orderOf_pos
  -- üéâ no goals
#align monoid.exponent_eq_max'_order_of Monoid.exponent_eq_max'_orderOf
#align add_monoid.exponent_eq_max'_order_of AddMonoid.exponent_eq_max'_addOrderOf

end CancelCommMonoid

end Monoid

section CommGroup

open Subgroup

open BigOperators

variable (G) [CommGroup G] [Group.FG G]

@[to_additive]
theorem card_dvd_exponent_pow_rank : Nat.card G ‚à£ Monoid.exponent G ^ Group.rank G := by
  obtain ‚ü®S, hS1, hS2‚ü© := Group.rank_spec G
  -- ‚ä¢ Nat.card G ‚à£ Monoid.exponent G ^ Group.rank G
  rw [‚Üê hS1, ‚Üê Fintype.card_coe, ‚Üê Finset.card_univ, ‚Üê Finset.prod_const]
  -- ‚ä¢ Nat.card G ‚à£ ‚àè _x : { x // x ‚àà S }, Monoid.exponent G
  let f : (‚àÄ g : S, zpowers (g : G)) ‚Üí* G := noncommPiCoprod fun s t _ x y _ _ => mul_comm x _
  -- ‚ä¢ Nat.card G ‚à£ ‚àè _x : { x // x ‚àà S }, Monoid.exponent G
  have hf : Function.Surjective f := by
    rw [‚Üê MonoidHom.range_top_iff_surjective, eq_top_iff, ‚Üê hS2, closure_le]
    exact fun g hg => ‚ü®Pi.mulSingle ‚ü®g, hg‚ü© ‚ü®g, mem_zpowers g‚ü©, noncommPiCoprod_mulSingle _ _‚ü©
  replace hf := nat_card_dvd_of_surjective f hf
  -- ‚ä¢ Nat.card G ‚à£ ‚àè _x : { x // x ‚àà S }, Monoid.exponent G
  rw [Nat.card_pi] at hf
  -- ‚ä¢ Nat.card G ‚à£ ‚àè _x : { x // x ‚àà S }, Monoid.exponent G
  refine' hf.trans (Finset.prod_dvd_prod_of_dvd _ _ fun g _ => _)
  -- ‚ä¢ Nat.card { x // x ‚àà zpowers ‚Üëg } ‚à£ Monoid.exponent G
  rw [‚Üê order_eq_card_zpowers']
  -- ‚ä¢ orderOf ‚Üëg ‚à£ Monoid.exponent G
  exact Monoid.order_dvd_exponent (g : G)
  -- üéâ no goals
#align card_dvd_exponent_pow_rank card_dvd_exponent_pow_rank
#align card_dvd_exponent_nsmul_rank card_dvd_exponent_nsmul_rank

@[to_additive]
theorem card_dvd_exponent_pow_rank' {n : ‚Ñï} (hG : ‚àÄ g : G, g ^ n = 1) :
    Nat.card G ‚à£ n ^ Group.rank G :=
  (card_dvd_exponent_pow_rank G).trans
    (pow_dvd_pow_of_dvd (Monoid.exponent_dvd_of_forall_pow_eq_one G n hG) (Group.rank G))
#align card_dvd_exponent_pow_rank' card_dvd_exponent_pow_rank'
#align card_dvd_exponent_nsmul_rank' card_dvd_exponent_nsmul_rank'

end CommGroup

