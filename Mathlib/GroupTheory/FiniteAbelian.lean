/-
Copyright (c) 2022 Pierre-Alexandre Bazin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Pierre-Alexandre Bazin
-/
import Mathlib.Algebra.Module.PID
import Mathlib.Data.ZMod.Quotient

#align_import group_theory.finite_abelian from "leanprover-community/mathlib"@"879155bff5af618b9062cbb2915347dafd749ad6"

/-!
# Structure of finite(ly generated) abelian groups

* `AddCommGroup.equiv_free_prod_directSum_zmod` : Any finitely generated abelian group is the
  product of a power of `‚Ñ§` and a direct sum of some `ZMod (p i ^ e i)` for some prime powers
  `p i ^ e i`.
* `AddCommGroup.equiv_directSum_zmod_of_fintype` : Any finite abelian group is a direct sum of
  some `ZMod (p i ^ e i)` for some prime powers `p i ^ e i`.

-/


open scoped DirectSum

universe u

namespace Module

variable (M : Type u)

theorem finite_of_fg_torsion [AddCommGroup M] [Module ‚Ñ§ M] [Module.Finite ‚Ñ§ M]
    (hM : Module.IsTorsion ‚Ñ§ M) : _root_.Finite M := by
  rcases Module.equiv_directSum_of_isTorsion hM with ‚ü®Œπ, _, p, h, e, ‚ü®l‚ü©‚ü©
  -- ‚ä¢ _root_.Finite M
  haveI : ‚àÄ i : Œπ, NeZero (p i ^ e i).natAbs := fun i =>
    ‚ü®Int.natAbs_ne_zero.mpr <| pow_ne_zero (e i) (h i).ne_zero‚ü©
  haveI : ‚àÄ i : Œπ, _root_.Finite <| ‚Ñ§ ‚ß∏ Submodule.span ‚Ñ§ {p i ^ e i} := fun i =>
    Finite.of_equiv _ (p i ^ e i).quotientSpanEquivZMod.symm.toEquiv
  haveI : _root_.Finite (‚®Å i, ‚Ñ§ ‚ß∏ (Submodule.span ‚Ñ§ {p i ^ e i} : Submodule ‚Ñ§ ‚Ñ§)) :=
    Finite.of_equiv _ DFinsupp.equivFunOnFintype.symm
  exact Finite.of_equiv _ l.symm.toEquiv
  -- üéâ no goals
#align module.finite_of_fg_torsion Module.finite_of_fg_torsion

end Module

variable (G : Type u)

namespace AddCommGroup

variable [AddCommGroup G]

/-- **Structure theorem of finitely generated abelian groups** : Any finitely generated abelian
group is the product of a power of `‚Ñ§` and a direct sum of some `ZMod (p i ^ e i)` for some
prime powers `p i ^ e i`. -/
theorem equiv_free_prod_directSum_zmod [hG : AddGroup.FG G] :
    ‚àÉ (n : ‚Ñï) (Œπ : Type) (_ : Fintype Œπ) (p : Œπ ‚Üí ‚Ñï) (_ : ‚àÄ i, Nat.Prime <| p i) (e : Œπ ‚Üí ‚Ñï),
      Nonempty <| G ‚âÉ+ (Fin n ‚Üí‚ÇÄ ‚Ñ§) √ó ‚®Å i : Œπ, ZMod (p i ^ e i) := by
  obtain ‚ü®n, Œπ, fŒπ, p, hp, e, ‚ü®f‚ü©‚ü© :=
    @Module.equiv_free_prod_directSum _ _ _ _ _ _ _ (Module.Finite.iff_addGroup_fg.mpr hG)
  refine' ‚ü®n, Œπ, fŒπ, fun i => (p i).natAbs, fun i => _, e, ‚ü®_‚ü©‚ü©
  -- ‚ä¢ Nat.Prime ((fun i => Int.natAbs (p i)) i)
  ¬∑ rw [‚Üê Int.prime_iff_natAbs_prime, ‚Üê GCDMonoid.irreducible_iff_prime]; exact hp i
    -- ‚ä¢ Irreducible (p i)
                                                                          -- üéâ no goals
  exact
    f.toAddEquiv.trans
      ((AddEquiv.refl _).prodCongr <|
        DFinsupp.mapRange.addEquiv fun i =>
          ((Int.quotientSpanEquivZMod _).trans <|
              ZMod.ringEquivCongr <| (p i).natAbs_pow _).toAddEquiv)
#align add_comm_group.equiv_free_prod_direct_sum_zmod AddCommGroup.equiv_free_prod_directSum_zmod

/-- **Structure theorem of finite abelian groups** : Any finite abelian group is a direct sum of
some `ZMod (p i ^ e i)` for some prime powers `p i ^ e i`. -/
theorem equiv_directSum_zmod_of_fintype [Finite G] :
    ‚àÉ (Œπ : Type) (_ : Fintype Œπ) (p : Œπ ‚Üí ‚Ñï) (_ : ‚àÄ i, Nat.Prime <| p i) (e : Œπ ‚Üí ‚Ñï),
      Nonempty <| G ‚âÉ+ ‚®Å i : Œπ, ZMod (p i ^ e i) := by
  cases nonempty_fintype G
  -- ‚ä¢ ‚àÉ Œπ x p x e, Nonempty (G ‚âÉ+ ‚®Å (i : Œπ), ZMod (p i ^ e i))
  obtain ‚ü®n, Œπ, fŒπ, p, hp, e, ‚ü®f‚ü©‚ü© := equiv_free_prod_directSum_zmod G
  -- ‚ä¢ ‚àÉ Œπ x p x e, Nonempty (G ‚âÉ+ ‚®Å (i : Œπ), ZMod (p i ^ e i))
  cases' n with n
  -- ‚ä¢ ‚àÉ Œπ x p x e, Nonempty (G ‚âÉ+ ‚®Å (i : Œπ), ZMod (p i ^ e i))
  ¬∑ have : Unique (Fin Nat.zero ‚Üí‚ÇÄ ‚Ñ§) :=
      { uniq := by simp only [Nat.zero_eq, eq_iff_true_of_subsingleton] }
    exact ‚ü®Œπ, fŒπ, p, hp, e, ‚ü®f.trans AddEquiv.uniqueProd‚ü©‚ü©
    -- üéâ no goals
  ¬∑ haveI := @Fintype.prodLeft _ _ _ (Fintype.ofEquiv G f.toEquiv) _
    -- ‚ä¢ ‚àÉ Œπ x p x e, Nonempty (G ‚âÉ+ ‚®Å (i : Œπ), ZMod (p i ^ e i))
    exact
      (Fintype.ofSurjective (fun f : Fin n.succ ‚Üí‚ÇÄ ‚Ñ§ => f 0) fun a =>
            ‚ü®Finsupp.single 0 a, Finsupp.single_eq_same‚ü©).false.elim
#align add_comm_group.equiv_direct_sum_zmod_of_fintype AddCommGroup.equiv_directSum_zmod_of_fintype

theorem finite_of_fg_torsion [hG' : AddGroup.FG G] (hG : AddMonoid.IsTorsion G) : Finite G :=
  @Module.finite_of_fg_torsion _ _ _ (Module.Finite.iff_addGroup_fg.mpr hG') <|
    AddMonoid.isTorsion_iff_isTorsion_int.mp hG
#align add_comm_group.finite_of_fg_torsion AddCommGroup.finite_of_fg_torsion

end AddCommGroup

namespace CommGroup

theorem finite_of_fg_torsion [CommGroup G] [Group.FG G] (hG : Monoid.IsTorsion G) : Finite G :=
  @Finite.of_equiv _ _ (AddCommGroup.finite_of_fg_torsion (Additive G) hG) Multiplicative.ofAdd
#align comm_group.finite_of_fg_torsion CommGroup.finite_of_fg_torsion

end CommGroup
