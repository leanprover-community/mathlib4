/-
Copyright (c) 2019 Michael Howes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Howes
-/
import Mathlib.GroupTheory.FreeGroup
import Mathlib.GroupTheory.QuotientGroup

#align_import group_theory.presented_group from "leanprover-community/mathlib"@"d90e4e186f1d18e375dcd4e5b5f6364b01cb3e46"

/-!
# Defining a group given by generators and relations

Given a subset `rels` of relations of the free group on a type `Î±`, this file constructs the group
given by generators `x : Î±` and relations `r âˆˆ rels`.

## Main definitions

* `PresentedGroup rels`: the quotient group of the free group on a type `Î±` by a subset `rels` of
  relations of the free group on `Î±`.
* `of`: The canonical map from `Î±` to a presented group with generators `Î±`.
* `toGroup f`: the canonical group homomorphism `PresentedGroup rels â†’ G`, given a function
  `f : Î± â†’ G` from a type `Î±` to a group `G` which satisfies the relations `rels`.

## Tags

generators, relations, group presentations
-/


variable {Î± : Type*}

/-- Given a set of relations, `rels`, over a type `Î±`, `PresentedGroup` constructs the group with
generators `x : Î±` and relations `rels` as a quotient of `FreeGroup Î±`. -/
def PresentedGroup (rels : Set (FreeGroup Î±)) :=
  FreeGroup Î± â§¸ Subgroup.normalClosure rels
#align presented_group PresentedGroup

namespace PresentedGroup

instance (rels : Set (FreeGroup Î±)) : Group (PresentedGroup rels) :=
  QuotientGroup.Quotient.group _

/-- `of` is the canonical map from `Î±` to a presented group with generators `x : Î±`. The term `x` is
mapped to the equivalence class of the image of `x` in `FreeGroup Î±`. -/
def of {rels : Set (FreeGroup Î±)} (x : Î±) : PresentedGroup rels :=
  QuotientGroup.mk (FreeGroup.of x)
#align presented_group.of PresentedGroup.of

section ToGroup

/-
Presented groups satisfy a universal property. If `G` is a group and `f : Î± â†’ G` is a map such that
the images of `f` satisfy all the given relations, then `f` extends uniquely to a group homomorphism
from `PresentedGroup rels` to `G`.
-/
variable {G : Type*} [Group G] {f : Î± â†’ G} {rels : Set (FreeGroup Î±)}

-- mathport name: exprF
local notation "F" => FreeGroup.lift f

-- Porting note: `F` has been expanded, because `F r = 1` produces a sorry.
variable (h : âˆ€ r âˆˆ rels, FreeGroup.lift f r = 1)

theorem closure_rels_subset_ker : Subgroup.normalClosure rels â‰¤ MonoidHom.ker F :=
  Subgroup.normalClosure_le_normal fun x w â†¦ (MonoidHom.mem_ker _).2 (h x w)
#align presented_group.closure_rels_subset_ker PresentedGroup.closure_rels_subset_ker

theorem to_group_eq_one_of_mem_closure : âˆ€ x âˆˆ Subgroup.normalClosure rels, F x = 1 :=
  fun _ w â†¦ (MonoidHom.mem_ker _).1 <| closure_rels_subset_ker h w
#align presented_group.to_group_eq_one_of_mem_closure PresentedGroup.to_group_eq_one_of_mem_closure

/-- The extension of a map `f : Î± â†’ G` that satisfies the given relations to a group homomorphism
from `PresentedGroup rels â†’ G`. -/
def toGroup : PresentedGroup rels â†’* G :=
  QuotientGroup.lift (Subgroup.normalClosure rels) F (to_group_eq_one_of_mem_closure h)
#align presented_group.to_group PresentedGroup.toGroup

@[simp]
theorem toGroup.of {x : Î±} : toGroup h (of x) = f x :=
  FreeGroup.lift.of
#align presented_group.to_group.of PresentedGroup.toGroup.of

theorem toGroup.unique (g : PresentedGroup rels â†’* G)
    (hg : âˆ€ x : Î±, g (PresentedGroup.of x) = f x) : âˆ€ {x}, g x = toGroup h x := by
  intro x
  -- âŠ¢ â†‘g x = â†‘(toGroup h) x
  refine' QuotientGroup.induction_on x _
  -- âŠ¢ âˆ€ (z : FreeGroup Î±), â†‘g â†‘z = â†‘(toGroup h) â†‘z
  exact fun _ â†¦ FreeGroup.lift.unique (g.comp (QuotientGroup.mk' _)) hg
  -- ðŸŽ‰ no goals
#align presented_group.to_group.unique PresentedGroup.toGroup.unique

end ToGroup

instance (rels : Set (FreeGroup Î±)) : Inhabited (PresentedGroup rels) :=
  âŸ¨1âŸ©

end PresentedGroup
