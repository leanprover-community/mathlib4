/-
Copyright (c) 2022 Joachim Breitner. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joachim Breitner
-/
import Mathlib.GroupTheory.OrderOfElement
import Mathlib.Data.Finset.NoncommProd
import Mathlib.Data.Fintype.BigOperators
import Mathlib.Data.Nat.GCD.BigOperators
import Mathlib.Order.SupIndep

#align_import group_theory.noncomm_pi_coprod from "leanprover-community/mathlib"@"6f9f36364eae3f42368b04858fd66d6d9ae730d8"

/-!
# Canonical homomorphism from a finite family of monoids

This file defines the construction of the canonical homomorphism from a family of monoids.

Given a family of morphisms `œï i : N i ‚Üí* M` for each `i : Œπ` where elements in the
images of different morphisms commute, we obtain a canonical morphism
`MonoidHom.noncommPiCoprod : (Œ† i, N i) ‚Üí* M` that coincides with `œï`

## Main definitions

* `MonoidHom.noncommPiCoprod : (Œ† i, N i) ‚Üí* M` is the main homomorphism
* `Subgroup.noncommPiCoprod : (Œ† i, H i) ‚Üí* G` is the specialization to `H i : Subgroup G`
   and the subgroup embedding.

## Main theorems

* `MonoidHom.noncommPiCoprod` coincides with `œï i` when restricted to `N i`
* `MonoidHom.noncommPiCoprod_mrange`: The range of `MonoidHom.noncommPiCoprod` is
  `‚®Ü (i : Œπ), (œï i).mrange`
* `MonoidHom.noncommPiCoprod_range`: The range of `MonoidHom.noncommPiCoprod` is
  `‚®Ü (i : Œπ), (œï i).range`
* `Subgroup.noncommPiCoprod_range`: The range of `Subgroup.noncommPiCoprod` is `‚®Ü (i : Œπ), H i`.
* `MonoidHom.injective_noncommPiCoprod_of_independent`: in the case of groups, `pi_hom.hom` is
   injective if the `œï` are injective and the ranges of the `œï` are independent.
* `MonoidHom.independent_range_of_coprime_order`: If the `N i` have coprime orders, then the ranges
   of the `œï` are independent.
* `Subgroup.independent_of_coprime_order`: If commuting normal subgroups `H i` have coprime orders,
   they are independent.

-/


open BigOperators

namespace Subgroup

variable {G : Type*} [Group G]

/-- `Finset.noncommProd` is ‚Äúinjective‚Äù in `f` if `f` maps into independent subgroups.  This
generalizes (one direction of) `Subgroup.disjoint_iff_mul_eq_one`. -/
@[to_additive "`Finset.noncommSum` is ‚Äúinjective‚Äù in `f` if `f` maps into independent subgroups.
This generalizes (one direction of) `AddSubgroup.disjoint_iff_add_eq_zero`. "]
theorem eq_one_of_noncommProd_eq_one_of_independent {Œπ : Type*} (s : Finset Œπ) (f : Œπ ‚Üí G) (comm)
    (K : Œπ ‚Üí Subgroup G) (hind : CompleteLattice.Independent K) (hmem : ‚àÄ x ‚àà s, f x ‚àà K x)
    (heq1 : s.noncommProd f comm = 1) : ‚àÄ i ‚àà s, f i = 1 := by
  classical
    revert heq1
    induction' s using Finset.induction_on with i s hnmem ih
    ¬∑ simp
    ¬∑ have hcomm := comm.mono (Finset.coe_subset.2 <| Finset.subset_insert _ _)
      simp only [Finset.forall_mem_insert] at hmem
      have hmem_bsupr : s.noncommProd f hcomm ‚àà ‚®Ü i ‚àà (s : Set Œπ), K i := by
        refine' Subgroup.noncommProd_mem _ _ _
        intro x hx
        have : K x ‚â§ ‚®Ü i ‚àà (s : Set Œπ), K i := le_iSup‚ÇÇ (f := fun i _ => K i) x hx
        exact this (hmem.2 x hx)
      intro heq1
      rw [Finset.noncommProd_insert_of_not_mem _ _ _ _ hnmem] at heq1
      have hnmem' : i ‚àâ (s : Set Œπ) := by simpa
      obtain ‚ü®heq1i : f i = 1, heq1S : s.noncommProd f _ = 1‚ü© :=
        Subgroup.disjoint_iff_mul_eq_one.mp (hind.disjoint_biSup hnmem') hmem.1 hmem_bsupr heq1
      intro i h
      simp only [Finset.mem_insert] at h
      rcases h with (rfl | h)
      ¬∑ exact heq1i
      ¬∑ refine' ih hcomm hmem.2 heq1S _ h
#align subgroup.eq_one_of_noncomm_prod_eq_one_of_independent Subgroup.eq_one_of_noncommProd_eq_one_of_independent
#align add_subgroup.eq_zero_of_noncomm_sum_eq_zero_of_independent AddSubgroup.eq_zero_of_noncommSum_eq_zero_of_independent

end Subgroup

section FamilyOfMonoids

variable {M : Type*} [Monoid M]

-- We have a family of monoids
-- The fintype assumption is not always used, but declared here, to keep things in order
variable {Œπ : Type*} [hdec : DecidableEq Œπ] [Fintype Œπ]

variable {N : Œπ ‚Üí Type*} [‚àÄ i, Monoid (N i)]

-- And morphisms œï into G
variable (œï : ‚àÄ i : Œπ, N i ‚Üí* M)

-- We assume that the elements of different morphism commute
variable (hcomm : Pairwise fun i j => ‚àÄ x y, Commute (œï i x) (œï j y))

-- We use `f` and `g` to denote elements of `Œ† (i : Œπ), N i`
variable (f g : ‚àÄ i : Œπ, N i)

namespace MonoidHom

/-- The canonical homomorphism from a family of monoids. -/
@[to_additive "The canonical homomorphism from a family of additive monoids. See also
`LinearMap.lsum` for a linear version without the commutativity assumption."]
def noncommPiCoprod : (‚àÄ i : Œπ, N i) ‚Üí* M
    where
  toFun f := Finset.univ.noncommProd (fun i => œï i (f i)) fun i _ j _ h => hcomm h _ _
  map_one' := by
    apply (Finset.noncommProd_eq_pow_card _ _ _ _ _).trans (one_pow _)
    -- ‚ä¢ ‚àÄ (x : Œπ), x ‚àà Finset.univ ‚Üí ‚Üë(œï x) (OfNat.ofNat 1 x) = 1
    simp
    -- üéâ no goals
  map_mul' f g := by
    classical
    simp only
    convert @Finset.noncommProd_mul_distrib _ _ _ _ (fun i => œï i (f i)) (fun i => œï i (g i)) _ _ _
    ¬∑ exact map_mul _ _ _
    ¬∑ rintro i - j - h
      exact hcomm h _ _
#align monoid_hom.noncomm_pi_coprod MonoidHom.noncommPiCoprod
#align add_monoid_hom.noncomm_pi_coprod AddMonoidHom.noncommPiCoprod

variable {hcomm}

@[to_additive (attr := simp)]
theorem noncommPiCoprod_mulSingle (i : Œπ) (y : N i) :
    noncommPiCoprod œï hcomm (Pi.mulSingle i y) = œï i y := by
  change Finset.univ.noncommProd (fun j => œï j (Pi.mulSingle i y j)) (fun _ _ _ _ h => hcomm h _ _)
    = œï i y
  simp (config := { singlePass := true }) only [‚Üê Finset.insert_erase (Finset.mem_univ i)]
  -- ‚ä¢ Finset.noncommProd (insert i (Finset.erase Finset.univ i)) (fun x => ‚Üë(œï x)  ‚Ä¶
  rw [Finset.noncommProd_insert_of_not_mem _ _ _ _ (Finset.not_mem_erase i _)]
  -- ‚ä¢ ‚Üë(œï i) (Pi.mulSingle i y i) * Finset.noncommProd (Finset.erase Finset.univ i ‚Ä¶
  rw [Pi.mulSingle_eq_same]
  -- ‚ä¢ ‚Üë(œï i) y * Finset.noncommProd (Finset.erase Finset.univ i) (fun x => ‚Üë(œï x)  ‚Ä¶
  rw [Finset.noncommProd_eq_pow_card]
  ¬∑ rw [one_pow]
    -- ‚ä¢ ‚Üë(œï i) y * 1 = ‚Üë(œï i) y
    exact mul_one _
    -- üéâ no goals
  ¬∑ intro j hj
    -- ‚ä¢ ‚Üë(œï j) (Pi.mulSingle i y j) = 1
    simp only [Finset.mem_erase] at hj
    -- ‚ä¢ ‚Üë(œï j) (Pi.mulSingle i y j) = 1
    simp [hj]
    -- üéâ no goals
#align monoid_hom.noncomm_pi_coprod_mul_single MonoidHom.noncommPiCoprod_mulSingle
#align add_monoid_hom.noncomm_pi_coprod_single AddMonoidHom.noncommPiCoprod_single

/-- The universal property of `MonoidHom.noncommPiCoprod` -/
@[to_additive "The universal property of `AddMonoidHom.noncommPiCoprod`"]
def noncommPiCoprodEquiv :
    { œï : ‚àÄ i, N i ‚Üí* M // Pairwise fun i j => ‚àÄ x y, Commute (œï i x) (œï j y) } ‚âÉ ((‚àÄ i, N i) ‚Üí* M)
    where
  toFun œï := noncommPiCoprod œï.1 œï.2
  invFun f :=
    ‚ü®fun i => f.comp (MonoidHom.single N i), fun i j hij x y =>
      Commute.map (Pi.mulSingle_commute hij x y) f‚ü©
  left_inv œï := by
    ext
    -- ‚ä¢ ‚Üë(‚Üë((fun f => { val := fun i => comp f (single N i), property := (_ : ‚àÄ (i j ‚Ä¶
    simp
    -- üéâ no goals
  right_inv f := pi_ext fun i x => by simp
                                      -- üéâ no goals
#align monoid_hom.noncomm_pi_coprod_equiv MonoidHom.noncommPiCoprodEquiv
#align add_monoid_hom.noncomm_pi_coprod_equiv AddMonoidHom.noncommPiCoprodEquiv

@[to_additive]
theorem noncommPiCoprod_mrange :
    MonoidHom.mrange (noncommPiCoprod œï hcomm) = ‚®Ü i : Œπ, MonoidHom.mrange (œï i) := by
  classical
    apply le_antisymm
    ¬∑ rintro x ‚ü®f, rfl‚ü©
      refine Submonoid.noncommProd_mem _ _ _ (fun _ _ _ _ h => hcomm h _ _) (fun i _ => ?_)
      apply Submonoid.mem_sSup_of_mem
      ¬∑ use i
      simp
    ¬∑ refine' iSup_le _
      rintro i x ‚ü®y, rfl‚ü©
      refine' ‚ü®Pi.mulSingle i y, noncommPiCoprod_mulSingle _ _ _‚ü©
#align monoid_hom.noncomm_pi_coprod_mrange MonoidHom.noncommPiCoprod_mrange
#align add_monoid_hom.noncomm_pi_coprod_mrange AddMonoidHom.noncommPiCoprod_mrange

end MonoidHom

end FamilyOfMonoids

section FamilyOfGroups

variable {G : Type*} [Group G]

variable {Œπ : Type*} [hdec : DecidableEq Œπ] [hfin : Fintype Œπ]

variable {H : Œπ ‚Üí Type*} [‚àÄ i, Group (H i)]

variable (œï : ‚àÄ i : Œπ, H i ‚Üí* G)

variable {hcomm : ‚àÄ i j : Œπ, i ‚â† j ‚Üí ‚àÄ (x : H i) (y : H j), Commute (œï i x) (œï j y)}

-- We use `f` and `g` to denote elements of `Œ† (i : Œπ), H i`
variable (f g : ‚àÄ i : Œπ, H i)

namespace MonoidHom

-- The subgroup version of `MonoidHom.noncommPiCoprod_mrange`
@[to_additive]
theorem noncommPiCoprod_range : (noncommPiCoprod œï hcomm).range = ‚®Ü i : Œπ, (œï i).range := by
  classical
    apply le_antisymm
    ¬∑ rintro x ‚ü®f, rfl‚ü©
      refine Subgroup.noncommProd_mem _ (fun _ _ _ _ h => hcomm _ _ h _ _) ?_
      intro i _hi
      apply Subgroup.mem_sSup_of_mem
      ¬∑ use i
      simp
    ¬∑ refine' iSup_le _
      rintro i x ‚ü®y, rfl‚ü©
      refine' ‚ü®Pi.mulSingle i y, noncommPiCoprod_mulSingle _ _ _‚ü©
#align monoid_hom.noncomm_pi_coprod_range MonoidHom.noncommPiCoprod_range
#align add_monoid_hom.noncomm_pi_coprod_range AddMonoidHom.noncommPiCoprod_range

@[to_additive]
theorem injective_noncommPiCoprod_of_independent
    (hind : CompleteLattice.Independent fun i => (œï i).range)
    (hinj : ‚àÄ i, Function.Injective (œï i)) : Function.Injective (noncommPiCoprod œï hcomm) := by
  classical
    apply (MonoidHom.ker_eq_bot_iff _).mp
    apply eq_bot_iff.mpr
    intro f heq1
    have : ‚àÄ i, i ‚àà Finset.univ ‚Üí œï i (f i) = 1 :=
      Subgroup.eq_one_of_noncommProd_eq_one_of_independent _ _ (fun _ _ _ _ h => hcomm _ _ h _ _)
        _ hind (by simp) heq1
    ext i
    apply hinj
    simp [this i (Finset.mem_univ i)]
#align monoid_hom.injective_noncomm_pi_coprod_of_independent MonoidHom.injective_noncommPiCoprod_of_independent
#align add_monoid_hom.injective_noncomm_pi_coprod_of_independent AddMonoidHom.injective_noncommPiCoprod_of_independent

variable (hcomm)

@[to_additive]
theorem independent_range_of_coprime_order [Finite Œπ] [‚àÄ i, Fintype (H i)]
    (hcoprime : ‚àÄ i j, i ‚â† j ‚Üí Nat.coprime (Fintype.card (H i)) (Fintype.card (H j))) :
    CompleteLattice.Independent fun i => (œï i).range := by
  cases nonempty_fintype Œπ
  -- ‚ä¢ CompleteLattice.Independent fun i => range (œï i)
  classical
    rintro i
    rw [disjoint_iff_inf_le]
    rintro f ‚ü®hxi, hxp‚ü©
    dsimp at hxi hxp
    rw [iSup_subtype', ‚Üê noncommPiCoprod_range] at hxp
    rotate_left
    ¬∑ intro _ _ hj
      apply hcomm
      exact hj ‚àò Subtype.ext
    cases' hxp with g hgf
    cases' hxi with g' hg'f
    have hxi : orderOf f ‚à£ Fintype.card (H i) := by
      rw [‚Üê hg'f]
      exact (orderOf_map_dvd _ _).trans orderOf_dvd_card_univ
    have hxp : orderOf f ‚à£ ‚àè j : { j // j ‚â† i }, Fintype.card (H j) := by
      rw [‚Üê hgf, ‚Üê Fintype.card_pi]
      exact (orderOf_map_dvd _ _).trans orderOf_dvd_card_univ
    change f = 1
    rw [‚Üê pow_one f, ‚Üê orderOf_dvd_iff_pow_eq_one]
    -- porting note: ouch, had to replace an ugly `convert`
    obtain ‚ü®c, hc‚ü© := Nat.dvd_gcd hxp hxi
    use c
    rw [‚Üê hc]
    symm
    rw [‚Üê Nat.coprime_iff_gcd_eq_one]
    apply Nat.coprime_prod_left
    intro j _
    apply hcoprime
    exact j.2
#align monoid_hom.independent_range_of_coprime_order MonoidHom.independent_range_of_coprime_order
#align add_monoid_hom.independent_range_of_coprime_order AddMonoidHom.independent_range_of_coprime_order

end MonoidHom

end FamilyOfGroups

namespace Subgroup

-- We have a family of subgroups
variable {G : Type*} [Group G]

variable {Œπ : Type*} [hdec : DecidableEq Œπ] [hfin : Fintype Œπ] {H : Œπ ‚Üí Subgroup G}

-- Elements of `Œ† (i : Œπ), H i` are called `f` and `g` here
variable (f g : ‚àÄ i : Œπ, H i)

section CommutingSubgroups

-- We assume that the elements of different subgroups commute
variable (hcomm : ‚àÄ i j : Œπ, i ‚â† j ‚Üí ‚àÄ x y : G, x ‚àà H i ‚Üí y ‚àà H j ‚Üí Commute x y)

@[to_additive]
theorem commute_subtype_of_commute (i j : Œπ) (hne : i ‚â† j) :
    ‚àÄ (x : H i) (y : H j), Commute ((H i).subtype x) ((H j).subtype y) := by
  rintro ‚ü®x, hx‚ü© ‚ü®y, hy‚ü©
  -- ‚ä¢ Commute (‚Üë(Subgroup.subtype (H i)) { val := x, property := hx }) (‚Üë(Subgroup ‚Ä¶
  exact hcomm i j hne x y hx hy
  -- üéâ no goals
#align subgroup.commute_subtype_of_commute Subgroup.commute_subtype_of_commute
#align add_subgroup.commute_subtype_of_commute AddSubgroup.commute_subtype_of_commute

/-- The canonical homomorphism from a family of subgroups where elements from different subgroups
commute -/
@[to_additive "The canonical homomorphism from a family of additive subgroups where elements from
different subgroups commute"]
def noncommPiCoprod : (‚àÄ i : Œπ, H i) ‚Üí* G :=
  MonoidHom.noncommPiCoprod (fun i => (H i).subtype) (commute_subtype_of_commute hcomm)
#align subgroup.noncomm_pi_coprod Subgroup.noncommPiCoprod
#align add_subgroup.noncomm_pi_coprod AddSubgroup.noncommPiCoprod

variable {hcomm}

@[to_additive (attr := simp)]
theorem noncommPiCoprod_mulSingle (i : Œπ) (y : H i) :
    noncommPiCoprod hcomm (Pi.mulSingle i y) = y := by apply MonoidHom.noncommPiCoprod_mulSingle
                                                       -- üéâ no goals
#align subgroup.noncomm_pi_coprod_mul_single Subgroup.noncommPiCoprod_mulSingle
#align add_subgroup.noncomm_pi_coprod_single AddSubgroup.noncommPiCoprod_single

@[to_additive]
theorem noncommPiCoprod_range : (noncommPiCoprod hcomm).range = ‚®Ü i : Œπ, H i := by
  simp [noncommPiCoprod, MonoidHom.noncommPiCoprod_range]
  -- üéâ no goals
#align subgroup.noncomm_pi_coprod_range Subgroup.noncommPiCoprod_range
#align add_subgroup.noncomm_pi_coprod_range AddSubgroup.noncommPiCoprod_range

@[to_additive]
theorem injective_noncommPiCoprod_of_independent (hind : CompleteLattice.Independent H) :
    Function.Injective (noncommPiCoprod hcomm) := by
  apply MonoidHom.injective_noncommPiCoprod_of_independent
  -- ‚ä¢ CompleteLattice.Independent fun i => MonoidHom.range (Subgroup.subtype ((fun ‚Ä¶
  ¬∑ simpa using hind
    -- üéâ no goals
  ¬∑ intro i
    -- ‚ä¢ Function.Injective ‚Üë(Subgroup.subtype ((fun i => H i) i))
    exact Subtype.coe_injective
    -- üéâ no goals
#align subgroup.injective_noncomm_pi_coprod_of_independent Subgroup.injective_noncommPiCoprod_of_independent
#align add_subgroup.injective_noncomm_pi_coprod_of_independent AddSubgroup.injective_noncommPiCoprod_of_independent

variable (hcomm)

@[to_additive]
theorem independent_of_coprime_order [Finite Œπ] [‚àÄ i, Fintype (H i)]
    (hcoprime : ‚àÄ i j, i ‚â† j ‚Üí Nat.coprime (Fintype.card (H i)) (Fintype.card (H j))) :
    CompleteLattice.Independent H := by
  simpa using
    MonoidHom.independent_range_of_coprime_order (fun i => (H i).subtype)
      (commute_subtype_of_commute hcomm) hcoprime
#align subgroup.independent_of_coprime_order Subgroup.independent_of_coprime_order
#align add_subgroup.independent_of_coprime_order AddSubgroup.independent_of_coprime_order

end CommutingSubgroups

end Subgroup
