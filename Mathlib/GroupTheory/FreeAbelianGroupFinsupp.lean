/-
Copyright (c) 2021 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/
import Mathlib.Algebra.Hom.Equiv.TypeTags
import Mathlib.Algebra.Module.Equiv
import Mathlib.Data.Finsupp.Defs
import Mathlib.GroupTheory.FreeAbelianGroup
import Mathlib.GroupTheory.IsFreeGroup
import Mathlib.LinearAlgebra.Dimension

#align_import group_theory.free_abelian_group_finsupp from "leanprover-community/mathlib"@"47b51515e69f59bca5cf34ef456e6000fe205a69"

/-!
# Isomorphism between `FreeAbelianGroup X` and `X ‚Üí‚ÇÄ ‚Ñ§`

In this file we construct the canonical isomorphism between `FreeAbelianGroup X` and `X ‚Üí‚ÇÄ ‚Ñ§`.
We use this to transport the notion of `support` from `Finsupp` to `FreeAbelianGroup`.

## Main declarations

- `FreeAbelianGroup.equivFinsupp`: group isomorphism between `FreeAbelianGroup X` and `X ‚Üí‚ÇÄ ‚Ñ§`
- `FreeAbelianGroup.coeff`: the multiplicity of `x : X` in `a : FreeAbelianGroup X`
- `FreeAbelianGroup.support`: the finset of `x : X` that occur in `a : FreeAbelianGroup X`

-/


noncomputable section

open BigOperators

variable {X : Type*}

/-- The group homomorphism `FreeAbelianGroup X ‚Üí+ (X ‚Üí‚ÇÄ ‚Ñ§)`. -/
def FreeAbelianGroup.toFinsupp : FreeAbelianGroup X ‚Üí+ X ‚Üí‚ÇÄ ‚Ñ§ :=
  FreeAbelianGroup.lift fun x => Finsupp.single x (1 : ‚Ñ§)
#align free_abelian_group.to_finsupp FreeAbelianGroup.toFinsupp

/-- The group homomorphism `(X ‚Üí‚ÇÄ ‚Ñ§) ‚Üí+ FreeAbelianGroup X`. -/
def Finsupp.toFreeAbelianGroup : (X ‚Üí‚ÇÄ ‚Ñ§) ‚Üí+ FreeAbelianGroup X :=
  Finsupp.liftAddHom fun x => (smulAddHom ‚Ñ§ (FreeAbelianGroup X)).flip (FreeAbelianGroup.of x)
#align finsupp.to_free_abelian_group Finsupp.toFreeAbelianGroup

open Finsupp FreeAbelianGroup

@[simp]
theorem Finsupp.toFreeAbelianGroup_comp_singleAddHom (x : X) :
    Finsupp.toFreeAbelianGroup.comp (Finsupp.singleAddHom x) =
      (smulAddHom ‚Ñ§ (FreeAbelianGroup X)).flip (of x) := by
  ext
  -- ‚ä¢ ‚Üë(AddMonoidHom.comp toFreeAbelianGroup (singleAddHom x)) 1 = ‚Üë(‚Üë(AddMonoidHo ‚Ä¶
  simp only [AddMonoidHom.coe_comp, Finsupp.singleAddHom_apply, Function.comp_apply, one_smul,
    toFreeAbelianGroup, Finsupp.liftAddHom_apply_single]
#align finsupp.to_free_abelian_group_comp_single_add_hom Finsupp.toFreeAbelianGroup_comp_singleAddHom

@[simp]
theorem FreeAbelianGroup.toFinsupp_comp_toFreeAbelianGroup :
    toFinsupp.comp toFreeAbelianGroup = AddMonoidHom.id (X ‚Üí‚ÇÄ ‚Ñ§) := by
  ext x y; simp only [AddMonoidHom.id_comp]
  -- ‚ä¢ ‚Üë(‚Üë(AddMonoidHom.comp (AddMonoidHom.comp toFinsupp toFreeAbelianGroup) (sing ‚Ä¶
           -- ‚ä¢ ‚Üë(‚Üë(AddMonoidHom.comp (AddMonoidHom.comp toFinsupp toFreeAbelianGroup) (sing ‚Ä¶
  rw [AddMonoidHom.comp_assoc, Finsupp.toFreeAbelianGroup_comp_singleAddHom]
  -- ‚ä¢ ‚Üë(‚Üë(AddMonoidHom.comp toFinsupp (‚Üë(AddMonoidHom.flip (smulAddHom ‚Ñ§ (FreeAbel ‚Ä¶
  simp only [toFinsupp, AddMonoidHom.coe_comp, Finsupp.singleAddHom_apply, Function.comp_apply,
    one_smul, lift.of, AddMonoidHom.flip_apply, smulAddHom_apply, AddMonoidHom.id_apply]
#align free_abelian_group.to_finsupp_comp_to_free_abelian_group FreeAbelianGroup.toFinsupp_comp_toFreeAbelianGroup

@[simp]
theorem Finsupp.toFreeAbelianGroup_comp_toFinsupp :
    toFreeAbelianGroup.comp toFinsupp = AddMonoidHom.id (FreeAbelianGroup X) := by
  ext
  -- ‚ä¢ ‚Üë(AddMonoidHom.comp toFreeAbelianGroup toFinsupp) (of x‚úù) = ‚Üë(AddMonoidHom.i ‚Ä¶
  rw [toFreeAbelianGroup, toFinsupp, AddMonoidHom.comp_apply, lift.of,
    liftAddHom_apply_single, AddMonoidHom.flip_apply, smulAddHom_apply, one_smul,
    AddMonoidHom.id_apply]
#align finsupp.to_free_abelian_group_comp_to_finsupp Finsupp.toFreeAbelianGroup_comp_toFinsupp

@[simp]
theorem Finsupp.toFreeAbelianGroup_toFinsupp {X} (x : FreeAbelianGroup X) :
    Finsupp.toFreeAbelianGroup (FreeAbelianGroup.toFinsupp x) = x := by
  rw [‚Üê AddMonoidHom.comp_apply, Finsupp.toFreeAbelianGroup_comp_toFinsupp, AddMonoidHom.id_apply]
  -- üéâ no goals
#align finsupp.to_free_abelian_group_to_finsupp Finsupp.toFreeAbelianGroup_toFinsupp

namespace FreeAbelianGroup

open Finsupp

@[simp]
theorem toFinsupp_of (x : X) : toFinsupp (of x) = Finsupp.single x 1 := by
  simp only [toFinsupp, lift.of]
  -- üéâ no goals
#align free_abelian_group.to_finsupp_of FreeAbelianGroup.toFinsupp_of

@[simp]
theorem toFinsupp_toFreeAbelianGroup (f : X ‚Üí‚ÇÄ ‚Ñ§) :
    FreeAbelianGroup.toFinsupp (Finsupp.toFreeAbelianGroup f) = f := by
  rw [‚Üê AddMonoidHom.comp_apply, toFinsupp_comp_toFreeAbelianGroup, AddMonoidHom.id_apply]
  -- üéâ no goals
#align free_abelian_group.to_finsupp_to_free_abelian_group FreeAbelianGroup.toFinsupp_toFreeAbelianGroup

variable (X)

/-- The additive equivalence between `FreeAbelianGroup X` and `(X ‚Üí‚ÇÄ ‚Ñ§)`. -/
@[simps!]
def equivFinsupp : FreeAbelianGroup X ‚âÉ+ (X ‚Üí‚ÇÄ ‚Ñ§) where
  toFun := toFinsupp
  invFun := toFreeAbelianGroup
  left_inv := toFreeAbelianGroup_toFinsupp
  right_inv := toFinsupp_toFreeAbelianGroup
  map_add' := toFinsupp.map_add
#align free_abelian_group.equiv_finsupp FreeAbelianGroup.equivFinsupp

/-- `A` is a basis of the ‚Ñ§-module `FreeAbelianGroup A`. -/
noncomputable def basis (Œ± : Type*) : Basis Œ± ‚Ñ§ (FreeAbelianGroup Œ±) :=
  ‚ü®(FreeAbelianGroup.equivFinsupp Œ±).toIntLinearEquiv‚ü©
#align free_abelian_group.basis FreeAbelianGroup.basis

/-- Isomorphic free abelian groups (as modules) have equivalent bases. -/
def Equiv.ofFreeAbelianGroupLinearEquiv {Œ± Œ≤ : Type*}
    (e : FreeAbelianGroup Œ± ‚âÉ‚Çó[‚Ñ§] FreeAbelianGroup Œ≤) : Œ± ‚âÉ Œ≤ :=
  let t : Basis Œ± ‚Ñ§ (FreeAbelianGroup Œ≤) := (FreeAbelianGroup.basis Œ±).map e
  t.indexEquiv <| FreeAbelianGroup.basis _
#align free_abelian_group.equiv.of_free_abelian_group_linear_equiv FreeAbelianGroup.Equiv.ofFreeAbelianGroupLinearEquiv

/-- Isomorphic free abelian groups (as additive groups) have equivalent bases. -/
def Equiv.ofFreeAbelianGroupEquiv {Œ± Œ≤ : Type*} (e : FreeAbelianGroup Œ± ‚âÉ+ FreeAbelianGroup Œ≤) :
    Œ± ‚âÉ Œ≤ :=
  Equiv.ofFreeAbelianGroupLinearEquiv e.toIntLinearEquiv
#align free_abelian_group.equiv.of_free_abelian_group_equiv FreeAbelianGroup.Equiv.ofFreeAbelianGroupEquiv

/-- Isomorphic free groups have equivalent bases. -/
def Equiv.ofFreeGroupEquiv {Œ± Œ≤ : Type*} (e : FreeGroup Œ± ‚âÉ* FreeGroup Œ≤) : Œ± ‚âÉ Œ≤ :=
  Equiv.ofFreeAbelianGroupEquiv (MulEquiv.toAdditive e.abelianizationCongr)
#align free_abelian_group.equiv.of_free_group_equiv FreeAbelianGroup.Equiv.ofFreeGroupEquiv

open IsFreeGroup

/-- Isomorphic free groups have equivalent bases (`IsFreeGroup` variant). -/
def Equiv.ofIsFreeGroupEquiv {G H : Type*} [Group G] [Group H] [IsFreeGroup G] [IsFreeGroup H]
    (e : G ‚âÉ* H) : Generators G ‚âÉ Generators H :=
  Equiv.ofFreeGroupEquiv <| MulEquiv.trans (toFreeGroup G).symm <| MulEquiv.trans e <| toFreeGroup H
#align free_abelian_group.equiv.of_is_free_group_equiv FreeAbelianGroup.Equiv.ofIsFreeGroupEquiv

variable {X}

/-- `coeff x` is the additive group homomorphism `FreeAbelianGroup X ‚Üí+ ‚Ñ§`
that sends `a` to the multiplicity of `x : X` in `a`. -/
def coeff (x : X) : FreeAbelianGroup X ‚Üí+ ‚Ñ§ :=
  (Finsupp.applyAddHom x).comp toFinsupp
#align free_abelian_group.coeff FreeAbelianGroup.coeff

/-- `support a` for `a : FreeAbelianGroup X` is the finite set of `x : X`
that occur in the formal sum `a`. -/
def support (a : FreeAbelianGroup X) : Finset X :=
  a.toFinsupp.support
#align free_abelian_group.support FreeAbelianGroup.support

theorem mem_support_iff (x : X) (a : FreeAbelianGroup X) : x ‚àà a.support ‚Üî coeff x a ‚â† 0 := by
  rw [support, Finsupp.mem_support_iff]
  -- ‚ä¢ ‚Üë(‚ÜëtoFinsupp a) x ‚â† 0 ‚Üî ‚Üë(coeff x) a ‚â† 0
  exact Iff.rfl
  -- üéâ no goals
#align free_abelian_group.mem_support_iff FreeAbelianGroup.mem_support_iff

theorem not_mem_support_iff (x : X) (a : FreeAbelianGroup X) : x ‚àâ a.support ‚Üî coeff x a = 0 := by
  rw [support, Finsupp.not_mem_support_iff]
  -- ‚ä¢ ‚Üë(‚ÜëtoFinsupp a) x = 0 ‚Üî ‚Üë(coeff x) a = 0
  exact Iff.rfl
  -- üéâ no goals
#align free_abelian_group.not_mem_support_iff FreeAbelianGroup.not_mem_support_iff

@[simp]
theorem support_zero : support (0 : FreeAbelianGroup X) = ‚àÖ := by
  simp only [support, Finsupp.support_zero, AddMonoidHom.map_zero]
  -- üéâ no goals
#align free_abelian_group.support_zero FreeAbelianGroup.support_zero

@[simp]
theorem support_of (x : X) : support (of x) = {x} := by
  rw [support, toFinsupp_of, Finsupp.support_single_ne_zero _ one_ne_zero]
  -- üéâ no goals
#align free_abelian_group.support_of FreeAbelianGroup.support_of

@[simp]
theorem support_neg (a : FreeAbelianGroup X) : support (-a) = support a := by
  simp only [support, AddMonoidHom.map_neg, Finsupp.support_neg]
  -- üéâ no goals
#align free_abelian_group.support_neg FreeAbelianGroup.support_neg

@[simp]
theorem support_zsmul (k : ‚Ñ§) (h : k ‚â† 0) (a : FreeAbelianGroup X) :
    support (k ‚Ä¢ a) = support a := by
  ext x
  -- ‚ä¢ x ‚àà support (k ‚Ä¢ a) ‚Üî x ‚àà support a
  simp only [mem_support_iff, AddMonoidHom.map_zsmul]
  -- ‚ä¢ k ‚Ä¢ ‚Üë(coeff x) a ‚â† 0 ‚Üî ‚Üë(coeff x) a ‚â† 0
  simp only [h, zsmul_int_int, false_or_iff, Ne.def, mul_eq_zero]
  -- üéâ no goals
#align free_abelian_group.support_zsmul FreeAbelianGroup.support_zsmul

@[simp]
theorem support_nsmul (k : ‚Ñï) (h : k ‚â† 0) (a : FreeAbelianGroup X) :
    support (k ‚Ä¢ a) = support a := by
  apply support_zsmul k _ a
  -- ‚ä¢ ‚Üëk ‚â† 0
  exact_mod_cast h
  -- üéâ no goals
#align free_abelian_group.support_nsmul FreeAbelianGroup.support_nsmul

open Classical

theorem support_add (a b : FreeAbelianGroup X) : support (a + b) ‚äÜ a.support ‚à™ b.support := by
  simp only [support, AddMonoidHom.map_add]
  -- ‚ä¢ (‚ÜëtoFinsupp a + ‚ÜëtoFinsupp b).support ‚äÜ (‚ÜëtoFinsupp a).support ‚à™ (‚ÜëtoFinsupp ‚Ä¶
  apply Finsupp.support_add
  -- üéâ no goals
#align free_abelian_group.support_add FreeAbelianGroup.support_add

end FreeAbelianGroup
