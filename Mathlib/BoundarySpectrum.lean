import Mathlib.Analysis.NormedSpace.Spectrum
import Mathlib.Analysis.NormedSpace.Star.Spectrum
import Mathlib.Data.Set.Subset
import Mathlib.Analysis.Complex.UpperHalfPlane.Topology

local notation "Ïƒ" => spectrum

namespace SubalgebraClass

section Algebra

variable {S R A : Type*} [CommSemiring R] [Semiring A] [Algebra R A]
variable [SetLike S A] [SubsemiringClass S A] [hSR : SMulMemClass S R A] (s : S)

instance (priority := 75) toAlgebra : Algebra R s where
  toFun r := âŸ¨algebraMap R A r, algebraMap_mem s râŸ©
  map_one' := Subtype.ext <| by simp
  map_mul' _ _ := Subtype.ext <| by simp
  map_zero' := Subtype.ext <| by simp
  map_add' _ _ := Subtype.ext <| by simp
  commutes' r x := Subtype.ext <| Algebra.commutes r (x : A)
  smul_def' r x := Subtype.ext <| (algebraMap_smul A r (x : A)).symm


/-- Embedding of a non-unital subalgebra into the non-unital algebra. -/
def val (s : S) : s â†’â‚[R] A :=
  { SubsemiringClass.subtype s, SMulMemClass.subtype s with
    toFun := (â†‘)
    commutes' := fun _ â†¦ rfl }

@[simp]
theorem coe_val : (val s : s â†’ A) = ((â†‘) : s â†’ A) :=
  rfl

end Algebra

section spectrum


variable {S R A : Type*} [CommSemiring R] [Ring A] [Algebra R A]
variable [SetLike S A] [SubringClass S A] [hSR : SMulMemClass S R A]

lemma _root_.spectrum.subset_subalgebra' {s : S} (a : s) :
    spectrum R (a : A) âŠ† spectrum R a :=
  Set.compl_subset_compl.mpr fun _ â†¦ IsUnit.map (val s)


end spectrum

section NormedAlgebra

variable {S R A : Type*} [NormedField R] [NormedRing A] [NormedAlgebra R A]
variable [SetLike S A] [SubringClass S A] [hSR : SMulMemClass S R A] (s : S)

instance (priority := 75) toNormedAlgebra : NormedAlgebra R s where
  norm_smul_le r x := norm_smul_le r (x : A)

end NormedAlgebra

end SubalgebraClass

section BoundarySpectrum

variable {ğ•œ A SubalgClass : Type*} [NormedField ğ•œ] [NormedRing A] [NormedAlgebra ğ•œ A] [CompleteSpace A]
variable [SetLike SubalgClass A] [SubringClass SubalgClass A] [SMulMemClass SubalgClass ğ•œ A]
variable (S : SubalgClass) [hS : IsClosed (S : Set A)] (x : S)

open Topology Filter

open SubalgebraClass in
/-- Let `S` be a closed subalgebra of a Banach algebra `A`. If `a : S` is invertible in `A`,
and for all `x : S` sufficiently close to `a` within some filter `l`, `x` is invertible in `S`,
then `a` is invertible in `S` as well. -/
lemma _root_.Subalgebra.isUnit_of_isUnit_val_of_eventually {l : Filter S} {a : S}
    (ha : IsUnit (a : A)) (hla : l â‰¤ ğ“ a) (hl : âˆ€á¶  x in l, IsUnit x) (hl' : l.NeBot) :
    IsUnit a := by
  have hlaâ‚‚ : Tendsto Ring.inverse (map (val S) l) (ğ“ (â†‘ha.unitâ»Â¹ : A)) := by
    rw [â† Ring.inverse_unit]
    exact (NormedRing.inverse_continuousAt _).tendsto.comp <|
      continuousAt_subtype_val.tendsto.comp <| map_mono hla
  suffices mem : (â†‘ha.unitâ»Â¹ : A) âˆˆ S by
    refine âŸ¨âŸ¨a, âŸ¨(â†‘ha.unitâ»Â¹ : A), memâŸ©, ?_, ?_âŸ©, rflâŸ©
    all_goals ext; simp
  apply hS.mem_of_tendsto hlaâ‚‚
  rw [Filter.eventually_map]
  apply hl.mp <| eventually_of_forall fun x hx â†¦ ?_
  suffices Ring.inverse (val S x) = (val S â†‘hx.unitâ»Â¹) from this â–¸ Subtype.property _
  have := (hx.map (val S)).unit_spec
  rw [â† (hx.map (val S)).unit_spec, Ring.inverse_unit (hx.map (val S)).unit, val]
  apply Units.mul_eq_one_iff_inv_eq.mp
  simpa [-IsUnit.mul_val_inv] using congr(($hx.mul_val_inv : A))

attribute [fun_prop] continuous_algebraMap

open Set

lemma _root_.Subalgebra.frontier_spectrum : frontier (spectrum ğ•œ x) âŠ† spectrum ğ•œ (x : A) := by
  have : CompleteSpace S := hS.completeSpace_coe
  intro Î¼ hÎ¼
  by_contra h
  rw [spectrum.not_mem_iff] at h
  rw [â† frontier_compl, IsOpen.frontier_eq, mem_diff] at hÎ¼
  swap
  Â· rw [spectrum, compl_compl]
    exact spectrum.isOpen_resolventSet (ğ•œ := ğ•œ) x
  obtain âŸ¨hÎ¼â‚, hÎ¼â‚‚âŸ© := hÎ¼
  rw [mem_closure_iff_clusterPt] at hÎ¼â‚
  apply hÎ¼â‚‚
  rw [mem_compl_iff, spectrum.not_mem_iff]
  refine Subalgebra.isUnit_of_isUnit_val_of_eventually S h ?_ ?_ <| .map hÎ¼â‚ (algebraMap ğ•œ S Â· - x)
  Â· calc
      _ â‰¤ map _ (ğ“ Î¼) := map_mono (by simp)
      _ â‰¤ _ := by rw [â† Filter.Tendsto, â† ContinuousAt]; fun_prop
  Â· rw [eventually_map]
    apply Eventually.filter_mono inf_le_right
    simp [spectrum.not_mem_iff]

lemma Subalgebra.frontier_subset_frontier :
    frontier (spectrum ğ•œ x) âŠ† frontier (spectrum ğ•œ (x : A)) := by
  rw [frontier_eq_closure_inter_closure (s := spectrum ğ•œ (x : A)),
    (spectrum.isClosed (x : A)).closure_eq]
  apply subset_inter (frontier_spectrum S x)
  rw [frontier_eq_closure_inter_closure]
  exact inter_subset_right _ _ |>.trans <|
    closure_mono <| compl_subset_compl.mpr <| spectrum.subset_subalgebra' x

open Set Notation

lemma isClopen_preimage_val {X : Type*} [TopologicalSpace X] {u v : Set X}
    (hu : IsOpen u) (huv : frontier u âˆ© v = âˆ…) : IsClopen (v â†“âˆ© u) := by
  refine âŸ¨?_, isOpen_induced hu (f := Subtype.val)âŸ©
  refine isClosed_induced_iff.mpr âŸ¨closure u, isClosed_closure, ?_âŸ©
  apply image_val_injective
  simp only [Subtype.image_preimage_coe]
  rw [closure_eq_self_union_frontier, inter_union_distrib_left, inter_comm _ (frontier u),
    huv, union_empty]

/-- If `u v : Set X` and `u âŠ† v` is clopen in `v`, then `u` is the union of the connected
components of `v` in `X` which intersect `u`. -/
lemma IsClopen.biUnion_connectedComponentIn {X : Type*} [TopologicalSpace X] {u v : Set X}
    (hu : IsClopen (v â†“âˆ© u)) (huvâ‚ : u âŠ† v) :
    u = â‹ƒ x âˆˆ u, connectedComponentIn v x := by
  have := congr(((â†‘) : Set v â†’ Set X) $(hu.biUnion_connectedComponent_eq.symm))
  simp only [Subtype.image_preimage_coe, mem_preimage, iUnion_coe_set, image_val_iUnion] at this
  rw [inter_eq_right.mpr huvâ‚] at this
  nth_rw 1 [this]
  congr! 2 with x hx
  simp only [â† connectedComponentIn_eq_image]
  exact le_antisymm (iUnion_subset fun _ â†¦ le_rfl) <|
    iUnion_subset fun hx â†¦ subset_iUnionâ‚‚_of_subset (huvâ‚ hx) hx le_rfl

example {X : Type*} [TopologicalSpace X] {u v : Set X} (hu : IsOpen u) (huvâ‚ : u âŠ† v)
    (huvâ‚‚ : frontier u âˆ© v = âˆ…) : u = â‹ƒ x âˆˆ u, connectedComponentIn v x :=
  isClopen_preimage_val hu huvâ‚‚ |>.biUnion_connectedComponentIn huvâ‚

lemma Set.diff_subset_compl {X : Type*} {u v : Set X} : u \ v âŠ† vá¶œ :=
  diff_eq_compl_inter â–¸ inter_subset_left _ _

/-- If `S` is a closed subalgebra of a Banach algebra `A`, then for any `x : S`, the spectrum of `x`
is the spectrum of `â†‘x : A` along with the connected components of the complement of the spectrum of
`â†‘x : A` which contain an element of the spectrum of `x : S`. -/
lemma Subalgebra.spectrum_sUnion_connectedComponentIn :
    Ïƒ ğ•œ x = Ïƒ ğ•œ (x : A) âˆª (â‹ƒ z âˆˆ (Ïƒ ğ•œ x \ Ïƒ ğ•œ (x : A)), connectedComponentIn (Ïƒ ğ•œ (x : A))á¶œ z) := by
  suffices IsClopen ((Ïƒ ğ•œ (x : A))á¶œ â†“âˆ© (Ïƒ ğ•œ x \ Ïƒ ğ•œ (x : A))) by
    rw [â† this.biUnion_connectedComponentIn diff_subset_compl,
      union_diff_cancel (spectrum.subset_subalgebra' x)]
  have : CompleteSpace S := hS.completeSpace_coe
  have h_open : IsOpen (Ïƒ ğ•œ x \ Ïƒ ğ•œ (x : A)) := by
    rw [â† (spectrum.isClosed (ğ•œ := ğ•œ) x).closure_eq, closure_eq_interior_union_frontier,
      union_diff_distrib, diff_eq_empty.mpr (frontier_spectrum S x),
      diff_eq_compl_inter, union_empty]
    exact (spectrum.isClosed _).isOpen_compl.inter isOpen_interior
  apply isClopen_preimage_val h_open
  suffices h_frontier : frontier (Ïƒ ğ•œ x \ Ïƒ ğ•œ (x : A)) âŠ† frontier (Ïƒ ğ•œ (x : A)) by
    rw [â† disjoint_iff_inter_eq_empty]
    exact disjoint_of_subset_left h_frontier <|
      disjoint_compl_right.frontier_left (spectrum.isClosed _).isOpen_compl
  rw [diff_eq_compl_inter]
  apply (frontier_inter_subset _ _).trans
  rw [frontier_compl]
  apply union_subset <| inter_subset_left _ _
  refine inter_subset_inter_right _ ?_ |>.trans <| inter_subset_right _ _
  exact frontier_subset_frontier S x

lemma Subalgebra.spectrum_isBounded_connectedComponentIn {z : ğ•œ} (hz : z âˆˆ Ïƒ ğ•œ x) :
    Bornology.IsBounded (connectedComponentIn (Ïƒ ğ•œ (x : A))á¶œ z) := by
  by_cases hz' : z âˆˆ Ïƒ ğ•œ (x : A)
  Â· rw [connectedComponentIn_eq_empty (show z âˆ‰ (Ïƒ ğ•œ (x : A))á¶œ from not_not.mpr hz')]
    exact Bornology.isBounded_empty
  Â· have : CompleteSpace S := hS.completeSpace_coe
    suffices connectedComponentIn (Ïƒ ğ•œ (x : A))á¶œ z âŠ† Ïƒ ğ•œ x from spectrum.isBounded x |>.subset this
    rw [spectrum_sUnion_connectedComponentIn S]
    exact subset_biUnion_of_mem (mem_diff_of_mem hz hz') |>.trans (subset_union_right _ _)

end BoundarySpectrum

section

variable {ğ•œ A SubalgClass : Type*} [NontriviallyNormedField ğ•œ] [NormedRing A] [NormedAlgebra ğ•œ A] [CompleteSpace A]
variable [SetLike SubalgClass A] [SubringClass SubalgClass A] [SMulMemClass SubalgClass ğ•œ A]
variable (S : SubalgClass) [hS : IsClosed (S : Set A)] (x : S)

open Set

/-- Let `S` be a closed subalgebra of a Banach algebra `A`. If for `x : S` the complement of the
spectrum of `â†‘x : A` is connected, then `spectrum ğ•œ x = spectrum ğ•œ (x : A)`. -/
lemma Subalgebra.spectrum_eq_of_isPreconnected_compl (h : IsPreconnected (Ïƒ ğ•œ (x : A))á¶œ) :
    Ïƒ ğ•œ x = Ïƒ ğ•œ (x : A) := by
  nontriviality A
  suffices Ïƒ ğ•œ x \ Ïƒ ğ•œ (x : A) = âˆ… by
    rw [spectrum_sUnion_connectedComponentIn, this]
    simp
  refine eq_empty_of_forall_not_mem fun z hz â†¦ ?_
  obtain âŸ¨hz, hz'âŸ© := mem_diff _ |>.mp hz
  have := (spectrum.isBounded (x : A)).union <|
    h.connectedComponentIn hz' â–¸ spectrum_isBounded_connectedComponentIn S x hz
  exact NormedSpace.unbounded_univ ğ•œ ğ•œ <| by simpa

end

section SpectralPermanence

variable {A : Type*} [NormedRing A] [NormedAlgebra â„‚ A] [CompleteSpace A] [StarRing A] [CstarRing A]

open Bornology

lemma Complex.isConnected_of_upperHalfPlane {s : Set â„‚} (hsâ‚ : {z | 0 < z.im} âŠ† s)
    (hsâ‚‚ : s âŠ† {z | 0 â‰¤ z.im}) : IsConnected s := by
  refine IsConnected.subset_closure ?_ hsâ‚ (by simpa using hsâ‚‚)
  rw [isConnected_iff_connectedSpace]
  exact inferInstanceAs (ConnectedSpace UpperHalfPlane)

lemma Complex.isConnected_of_lowerHalfPlane {s : Set â„‚} (hsâ‚ : {z | z.im < 0} âŠ† s)
    (hsâ‚‚ : s âŠ† {z | z.im â‰¤ 0}) : IsConnected s := by
  rw [â† Equiv.star.surjective.image_preimage s]
  refine IsConnected.image (f := Equiv.star) ?_ continuous_star.continuousOn
  apply Complex.isConnected_of_upperHalfPlane
  Â· exact fun z hz â†¦ hsâ‚ <| show star z âˆˆ _ by simpa
  Â· exact fun z hz â†¦ by simpa using show (star z).im â‰¤ 0 from hsâ‚‚ hz

lemma IsSelfAdjoint.isConnected_spectrum_compl [StarModule â„‚ A] {a : A} (ha : IsSelfAdjoint a) :
    IsConnected (Ïƒ â„‚ a)á¶œ := by
  suffices IsConnected (((Ïƒ â„‚ a)á¶œ âˆ© {z | 0 â‰¤ z.im}) âˆª (Ïƒ â„‚ a)á¶œ âˆ© {z | z.im â‰¤ 0}) by
    rw [â† Set.inter_union_distrib_left, â† Set.setOf_or] at this
    rw [â† Set.inter_univ (Ïƒ â„‚ a)á¶œ]
    convert this using 2
    exact Eq.symm <| Set.eq_univ_of_forall (fun z â†¦ le_total 0 z.im)
  refine IsConnected.union ?nonempty ?upper ?lower
  case nonempty =>
    have := Filter.NeBot.nonempty_of_mem inferInstance <| Filter.mem_map.mp <|
      Complex.isometry_ofReal.antilipschitz.tendsto_cobounded (spectrum.isBounded a |>.compl)
    exact this.image Complex.ofReal' |>.mono <| by simpa using Set.Subset.rfl
  case' upper => apply Complex.isConnected_of_upperHalfPlane ?_ <| Set.inter_subset_right _ _
  case' lower => apply Complex.isConnected_of_lowerHalfPlane ?_ <| Set.inter_subset_right _ _
  all_goals
    apply Set.subset_inter ?_ (fun z hz â†¦ by rw [Set.mem_setOf_eq] at hz âŠ¢; exact hz.le)
    intro z hz hz'
    rw [Set.mem_setOf_eq, ha.mem_spectrum_eq_re hz'] at hz
    simp_all

lemma StarSubalgebra.coe_isUnit [StarModule â„‚ A] (S : StarSubalgebra â„‚ A)
    [_hS : IsClosed (S : Set A)] {a : S} :
    IsUnit (a : A) â†” IsUnit a := by
  refine âŸ¨fun ha â†¦ ?_, IsUnit.map S.subtypeâŸ©
  have haâ‚ := ha.star.mul ha
  have haâ‚‚ := ha.mul ha.star
  have spec_eq {x : S} (hx : IsSelfAdjoint x) : spectrum â„‚ x = spectrum â„‚ (x : A) :=
    Subalgebra.spectrum_eq_of_isPreconnected_compl S _ <|
      (hx.starHom_apply S.subtype).isConnected_spectrum_compl.isPreconnected
  rw [â† StarMemClass.coe_star, â† MulMemClass.coe_mul, â† spectrum.zero_not_mem_iff â„‚, â† spec_eq,
    spectrum.zero_not_mem_iff] at haâ‚ haâ‚‚
  Â· have hâ‚ : haâ‚.unitâ»Â¹ * star a * a = 1 := mul_assoc _ _ a â–¸ haâ‚.val_inv_mul
    have hâ‚‚ : a * (star a * haâ‚‚.unitâ»Â¹) = 1 := (mul_assoc a _ _).symm â–¸ haâ‚‚.mul_val_inv
    exact âŸ¨âŸ¨a, haâ‚.unitâ»Â¹ * star a, left_inv_eq_right_inv hâ‚ hâ‚‚ â–¸ hâ‚‚, hâ‚âŸ©, rflâŸ©
  Â· exact IsSelfAdjoint.mul_star_self a
  Â· exact IsSelfAdjoint.star_mul_self a

lemma StarSubalgebra.mem_spectrum_iff [StarModule â„‚ A] (S : StarSubalgebra â„‚ A)
    [_hS : IsClosed (S : Set A)] {a : S} {z : â„‚} :
    z âˆˆ spectrum â„‚ a â†” z âˆˆ spectrum â„‚ (a : A) :=
  not_iff_not.2 S.coe_isUnit.symm

lemma StarSubalgebra.spectrum_eq [StarModule â„‚ A] (S : StarSubalgebra â„‚ A)
    [_hS : IsClosed (S : Set A)] {a : S} :
    spectrum â„‚ a = spectrum â„‚ (a : A) :=
  Set.ext fun _ â†¦ S.mem_spectrum_iff

end SpectralPermanence
