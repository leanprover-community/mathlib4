import Mathlib.Topology.MetricSpace.Polish
import Mathlib.MeasureTheory.Measure.Regular
import Mathlib.Logic.Denumerable
import Mathlib.KolmogorovExtension4.AuxLemmas

open Set MeasureTheory

open scoped ENNReal Topology BigOperators NNReal

section Misc

variable {Î± : Type _}

namespace Set

-- actually not used anymore
theorem monotone_iUnion {s : â„• â†’ Set Î±} (hs : Monotone s) (n : â„•) : (â‹ƒ m â‰¤ n, s m) = s n := by
  apply subset_antisymm
  Â· exact iUnion_subset fun m â†¦ iUnion_subset fun hm â†¦ hs hm
  Â· exact subset_iUnion_of_subset n (subset_iUnion_of_subset le_rfl subset_rfl)

-- actually not used anymore
theorem antitone_iInter {s : â„• â†’ Set Î±} (hs : Antitone s) (n : â„•) : (â‹‚ m â‰¤ n, s m) = s n := by
  apply subset_antisymm
  Â· exact iInter_subset_of_subset n (iInter_subset _ le_rfl)
  Â· exact subset_iInter fun i â†¦ subset_iInter fun hin â†¦ hs hin

theorem eq_iInter_iInter {s : â„• â†’ Set Î±} : (â‹‚ n, s n) = â‹‚ (n : â„•) (m : â„•) (_ : m â‰¤ n), s m := by
  ext x; simp only [Set.mem_iInter]; exact âŸ¨fun h _ k _ â†¦ h k, fun h i â†¦ h i i le_rflâŸ©

end Set

namespace Function

/-- For some set s in the domain and S' in the codomain of f, assume S' âŠ† f '' s.
Then, there is s' âŠ† s with S' = f '' s'. -/
theorem subset_image {Î± Î² : Type _} {f : Î± â†’ Î²} {s : Set Î±} {S' : Set Î²} (hS' : S' âŠ† f '' s) :
    âˆƒ (s' : Set Î±) (_ : s' âŠ† s), f '' s' = S' := by
  refine âŸ¨f â»Â¹' S' âˆ© s, Set.inter_subset_right, ?_âŸ©
  ext x
  simp only [mem_image, mem_inter_iff, mem_preimage]
  constructor
  Â· rintro âŸ¨y, âŸ¨hfy_mem, _âŸ©, rflâŸ©
    exact hfy_mem
  Â· intro h
    obtain âŸ¨y, hy_mem, rflâŸ© : x âˆˆ f '' s := hS' h
    exact âŸ¨y, âŸ¨h, hy_memâŸ©, rflâŸ©

/-- For some set s in the domain and a finset S' in the codomain of f, assume S' âŠ† f '' s.
Then, there is a finset s' âŠ† s with S' = f '' s'. -/
theorem subset_image_finset {Î± Î² : Type _} {f : Î± â†’ Î²} {s : Set Î±} {S' : Finset Î²}
    (hS'1 : â†‘S' âŠ† f '' s) : âˆƒ (s' : Finset Î±) (_ : â†‘s' âŠ† s), f '' s' = S' := by
  classical
  have h : âˆ€ x âˆˆ S', âˆƒ y : Î±, y âˆˆ s âˆ§ f y = x := fun x hx â†¦ (mem_image f s _).1 (hS'1 hx)
  choose g hg using h
  let g' : S' â†’ Î± := fun x â†¦ g x x.2
  refine âŸ¨(range g').toFinset, ?_, ?_âŸ©
  Â· intro x
    simp only [toFinset_range, Finset.univ_eq_attach, Finset.coe_image, mem_image, Finset.mem_coe, Finset.mem_attach,
      true_and, Subtype.exists, forall_exists_index]
    rintro y H rfl
    exact (hg y H).1
  Â· ext1 x
    simp only [toFinset_range, Finset.univ_eq_attach, Finset.coe_image, mem_image, Finset.mem_coe,
      Finset.mem_attach, true_and_iff, Finset.exists_coe]
    constructor
    Â· rintro âŸ¨y, âŸ¨x, hx_mem, rflâŸ©, rflâŸ©
      rwa [(hg x hx_mem).2]
    Â· intro h
      obtain âŸ¨y, _, rflâŸ© : x âˆˆ f '' s := hS'1 h
      exact âŸ¨g (f y) h, âŸ¨f y, h, rflâŸ©, (hg (f y) h).2âŸ©

/-- Same as subset_image, but assuming that S' is finite.
Then, s' can be chosen to be finite, too. -/
theorem subset_image_fintype {Î± Î² : Type _} {f : Î± â†’ Î²} {s : Set Î±} {S' : Set Î²}
    (hS'1 : S' âŠ† f '' s) (hS'2 : S'.Finite) :
    âˆƒ (s' : Set Î±) (_ : s' âŠ† s) (_ : s'.Finite), f '' s' = S' := by
  obtain âŸ¨s', hs', hfs'âŸ© :=
    @subset_image_finset Î± Î² f s hS'2.toFinset (by rwa [Finite.coe_toFinset])
  refine âŸ¨s', hs', Finset.finite_toSet s', ?_âŸ©
  rwa [Finite.coe_toFinset] at hfs'

end Function

namespace ENNReal

theorem tendsto_atTop_zero_iff_of_antitone (f : â„• â†’ â„â‰¥0âˆ) (hf : Antitone f) :
    Filter.Tendsto f Filter.atTop (ğ“ 0) â†” âˆ€ Îµ, 0 < Îµ â†’ âˆƒ n : â„•, f n â‰¤ Îµ := by
  rw [ENNReal.tendsto_atTop_zero]
  refine âŸ¨fun h â†¦ fun Îµ hÎµ â†¦ ?_, fun h â†¦ fun Îµ hÎµ â†¦ ?_âŸ©
  Â· obtain âŸ¨n, hnâŸ© := h Îµ hÎµ
    exact âŸ¨n, hn n le_rflâŸ©
  Â· obtain âŸ¨n, hnâŸ© := h Îµ hÎµ
    exact âŸ¨n, fun m hm â†¦ (hf hm).trans hnâŸ©

theorem tendsto_atTop_of_antitone (f : â„• â†’ â„â‰¥0âˆ) (hf : Antitone f) :
    Filter.Tendsto f Filter.atTop (ğ“ 0) â†” âˆ€ Îµ, 0 < Îµ â†’ âˆƒ n : â„•, f n < Îµ := by
  rw [ENNReal.tendsto_atTop_zero_iff_of_antitone f hf]
  constructor <;> intro h Îµ hÎµ
  have hÎµ' : (min 1 (Îµ / 2)) > 0 := by
    simp only [ge_iff_le, gt_iff_lt, lt_min_iff, zero_lt_one, div_pos_iff, ne_eq, and_true,
      true_and]
    simp only [two_ne_top, not_false_eq_true, and_true]
    intro g
    exact hÎµ.ne g.symm
  Â· obtain âŸ¨n, hnâŸ© := h (min 1 (Îµ / 2)) hÎµ'
    Â· refine âŸ¨n, hn.trans_lt ?_âŸ©
      by_cases hÎµ_top : Îµ = âˆ
      Â· rw [hÎµ_top]
        exact (min_le_left _ _).trans_lt ENNReal.one_lt_top
      refine (min_le_right _ _).trans_lt ?_
      rw [ENNReal.div_lt_iff (Or.inr hÎµ.ne') (Or.inr hÎµ_top)]
      conv_lhs => rw [â† mul_one Îµ]
      rw [ENNReal.mul_lt_mul_left hÎµ.ne' hÎµ_top]
      norm_num
  Â· obtain âŸ¨n, hnâŸ© := h Îµ hÎµ
    exact âŸ¨n, hn.leâŸ©

end ENNReal

end Misc

universe u

variable {Î± : Type u}

section MeasureTheory

namespace MeasureTheory

variable [MeasurableSpace Î±]

/-- Some version of continuity of a measure in the emptyset using a decreasing sequence of sets. -/
theorem cont_at_empty_of_measure (m : Measure Î±) [IsFiniteMeasure m] (s : â„• â†’ Set Î±)
    (hs1 : âˆ€ n, MeasurableSet (s n)) (hs2 : Antitone s) (hs3 : (â‹‚ n, s n) = âˆ…) :
    Filter.Tendsto (fun n â†¦ m (s n)) Filter.atTop (ğ“ 0) := by
  convert MeasureTheory.tendsto_measure_iInter hs1 hs2 _
  Â· rw [hs3]; exact measure_empty.symm
  Â· exact âŸ¨0, measure_ne_top m _âŸ©

theorem cont_at_empty_of_measure' (m : Measure Î±) [IsFiniteMeasure m] (s : â„• â†’ Set Î±)
    (hs1 : âˆ€ n, MeasurableSet (s n)) (hs2 : Antitone s) (hs3 : (â‹‚ n, s n) = âˆ…) :
    âˆ€ Îµ, 0 < Îµ â†’ âˆƒ n, m (s n) < Îµ :=
  (ENNReal.tendsto_atTop_of_antitone (fun n â†¦ m (s n)) fun _ _ h12 â†¦ measure_mono (hs2 h12)).1
    (cont_at_empty_of_measure m s hs1 hs2 hs3)

/-- Some version of continuity of a measure in the emptyset using the intersection along a set of
sets. -/
theorem continuous_at_emptyset_inter (m : Measure Î±) [IsFiniteMeasure m] (S : Set (Set Î±))
    (hS : Countable S) (hS2 : âˆ€ s âˆˆ S, MeasurableSet s) (hS3 : â‹‚â‚€ S = âˆ…) {Îµ : â„â‰¥0âˆ} (hÎµ : 0 < Îµ) :
    âˆƒ (S' : Set (Set Î±)) (_ : S'.Finite) (_ : S' âŠ† S), m (â‹‚â‚€ S') < Îµ := by
  simp only [countable_coe_iff] at hS
  cases' (fintypeOrInfinite S) with hS1 hS1
  Â· refine âŸ¨S, toFinite S, subset_rfl, ?_âŸ©
    rw [hS3, measure_empty]
    exact hÎµ
  Â· have hS' : Denumerable S :=
      @Denumerable.ofEncodableOfInfinite S (Set.Countable.toEncodable hS) hS1
    let e : S â‰ƒ â„• := Denumerable.eqv S
    let u n := ((e.symm n) : Set Î±)
    have hu_range : range u = S := by
      change range (Subtype.val âˆ˜ e.symm) = S
      rw [range_comp, Equiv.range_eq_univ]
      simp only [image_univ, Subtype.range_coe_subtype, setOf_mem_eq]
    have hu_meas : âˆ€ n, MeasurableSet (u n) := fun n â†¦ hS2 _ (Subtype.coe_prop _)
    let s n := (Set.Accumulate (fun m â†¦ ((u m)á¶œ : Set Î±)) n)á¶œ
    have hs1 : âˆ€ n, MeasurableSet (s n) := by
      refine fun n â†¦ (MeasurableSet.iUnion (fun b â†¦ MeasurableSet.iUnion (fun _ â†¦ ?_))).compl
      exact (hu_meas _).compl
    have hs2 : Antitone s := by
      intro n1 n2 h12
      simp only [s, le_eq_subset, compl_subset_compl]
      apply Set.monotone_accumulate h12
    have hs3 : â‹‚ (n : â„•), s n = âˆ… := by
      rw [Iff.symm compl_univ_iff]
      simp only [s, compl_iInter, compl_compl]
      rw [Set.iUnion_accumulate, â† compl_iInter, compl_univ_iff, â†hS3, â† Set.sInter_range, hu_range]
    obtain âŸ¨n, hnâŸ© : âˆƒ n, m (s n) < Îµ := cont_at_empty_of_measure' m s hs1 hs2 hs3 Îµ hÎµ
    let S' := u '' {m : â„• | m â‰¤ n}
    have S'_sub : S' âŠ† S := by
      rw [â† hu_range]
      exact image_subset_range _ _
    have h0 : (â‹‚â‚€ S') = s n := by
      simp only [S', s, Denumerable.decode_eq_ofNat, Option.some.injEq, sInter_image, mem_setOf_eq]
      rw [Set.accumulate_def]
      simp only [Denumerable.decode_eq_ofNat, Option.some.injEq, compl_iUnion, compl_compl]
    refine âŸ¨S', Set.Finite.image _ (toFinite _), S'_sub, ?_âŸ©
    rw [h0]
    exact hn

end MeasureTheory

end MeasureTheory

section RelativelyCompact

theorem of_compact [TopologicalSpace Î±] [T2Space Î±] {s : Set Î±} (hs : IsCompact s) :
    IsCompact (closure s) := by rwa [IsClosed.closure_eq hs.isClosed]

end RelativelyCompact

section Topology

namespace UniformSpace

def interUnionBalls (s' : â„• â†’ Set Î±) (V : â„• â†’ Set (Î± Ã— Î±)) : Set Î± :=
  â‹‚ n : â„•, â‹ƒ x âˆˆ s' n, UniformSpace.ball x (Prod.swap â»Â¹' V n)

theorem totallyBounded_interUnionBalls [UniformSpace Î±] {p : â„• â†’ Prop} {U : â„• â†’ Set (Î± Ã— Î±)}
    (H : (uniformity Î±).HasBasis p U) (s' : â„• â†’ Finset Î±) :
    TotallyBounded (interUnionBalls (fun n â†¦ â†‘(s' n)) U) := by
  rw [Filter.HasBasis.totallyBounded_iff H]
  intro i _
  let A := interUnionBalls (fun n â†¦ (s' n : Set Î±)) U
  have hA2 : A âŠ† â‹ƒ (x : Î±) (_ : x âˆˆ s' i), UniformSpace.ball x (Prod.swap â»Â¹' U i) := by
    exact fun x hx â†¦ Set.mem_iInter.1 hx i
  refine âŸ¨s' i, Finset.finite_toSet (s' i), ?_âŸ©
  simp only [Finset.mem_coe]
  simp only [UniformSpace.ball] at hA2
  intro x hx
  specialize hA2 hx
  let B x := Prod.mk x â»Â¹' (Prod.swap â»Â¹' U i)
  let C x := {y : Î± | (y, x) âˆˆ U i}
  have h : B = C := by ext x y; rfl
  change x âˆˆ â‹ƒ (x : Î±) (_ : x âˆˆ s' i), C x
  rw [â† h]
  exact hA2

/-- The construction of inter_union_balls is used to have a relatively compact set, as shown here.-/
theorem isCompact_closure_interUnionBalls [UniformSpace Î±] {p : â„• â†’ Prop} {U : â„• â†’ Set (Î± Ã— Î±)}
    (H : (uniformity Î±).HasBasis p U) [CompleteSpace Î±] (s' : â„• â†’ Finset Î±) :
    IsCompact (closure (interUnionBalls (fun n â†¦ (s' n : Set Î±)) U)) := by
  rw [isCompact_iff_totallyBounded_isComplete]
  refine âŸ¨TotallyBounded.closure ?_, IsClosed.isComplete isClosed_closureâŸ©
  exact totallyBounded_interUnionBalls H s'

end UniformSpace

end Topology

namespace MeasureTheory

variable [MeasurableSpace Î±]

theorem innerRegular_isCompact_is_rel_compact [TopologicalSpace Î±] (Î¼ : Measure Î±)
    (h : Î¼.InnerRegularWRT (fun s â†¦ IsCompact (closure s)) IsClosed) :
    Î¼.InnerRegularWRT IsCompact IsClosed := by
  intro A hA r hr
  rcases h hA r hr with âŸ¨K, âŸ¨hK1, hK2, hK3âŸ©âŸ©
  exact âŸ¨closure K, closure_minimal hK1 hA, hK2, hK3.trans_le (measure_mono subset_closure)âŸ©

theorem innerRegular_isCompact_is_rel_compact_iff [TopologicalSpace Î±] [T2Space Î±] (Î¼ : Measure Î±) :
    Î¼.InnerRegularWRT IsCompact IsClosed â†” Î¼.InnerRegularWRT (IsCompact âˆ˜ closure) IsClosed := by
  refine âŸ¨fun h A hA r hr â†¦ ?_, innerRegular_isCompact_is_rel_compact Î¼âŸ©
  rcases h hA r hr with âŸ¨K, âŸ¨hK1, hK2, hK3âŸ©âŸ©
  use closure K
  refine âŸ¨closure_minimal hK1 hA, ?_, ?_âŸ©
  Â· simp only [closure_closure, Function.comp_apply]; exact of_compact hK2
  Â· exact hK3.trans_le (measure_mono subset_closure)

theorem innerRegular_of_univ [TopologicalSpace Î±] [OpensMeasurableSpace Î±] (Î¼ : Measure Î±)
    (hÎ¼ : âˆ€ (Îµ : â„â‰¥0âˆ) (hÎµ : 0 < Îµ), âˆƒ (K : _) (_ : IsCompact (closure K)), Î¼ (Ká¶œ) < Îµ)
    [IsFiniteMeasure Î¼] : Î¼.InnerRegularWRT (IsCompact âˆ˜ closure) IsClosed := by
  intro A hA r hr
  obtain âŸ¨K, hK_relatively_compact, hKA, h_ltâŸ© :
    âˆƒ (K : _) (_ : IsCompact (closure K)) (_ : K âŠ† A), Î¼ (A \ closure K) < Î¼ A - r := by
    obtain âŸ¨K', hK'_relatively_compact, hK'_ltâŸ© := hÎ¼ (Î¼ A - r) (tsub_pos_of_lt hr)
    refine âŸ¨closure K' âˆ© A, ?_, âŸ¨inter_subset_right, ?_âŸ©âŸ©
    Â· rw [IsClosed.closure_eq]
      exact hK'_relatively_compact.inter_right hA
      apply IsClosed.inter isClosed_closure hA
    refine (measure_mono fun x â†¦ ?_).trans_lt hK'_lt
    simp only [diff_inter_self_eq_diff, mem_diff, mem_compl_iff, and_imp, imp_self, imp_true_iff]
    rw [IsClosed.closure_eq (IsClosed.inter isClosed_closure hA)]
    exact fun hA hK hK' â†¦ hK âŸ¨subset_closure hK', hAâŸ©
  refine âŸ¨closure K, closure_minimal hKA hA, ?_, ?_âŸ©
  Â· simp only [closure_closure, Function.comp_apply]
    exact hK_relatively_compact
  rw [measure_diff (closure_minimal hKA hA) _ (measure_ne_top Î¼ _)] at h_lt
  exact lt_of_tsub_lt_tsub_left h_lt
  exact measurableSet_closure

theorem innerRegular_of_univ' [TopologicalSpace Î±] [OpensMeasurableSpace Î±] (Î¼ : Measure Î±)
    (hÎ¼ : âˆ€ (Îµ : â„â‰¥0) (hÎµ : 0 < Îµ), âˆƒ (K : _) (_ : IsCompact (closure K)), Î¼ (Ká¶œ) < Îµ)
    [IsFiniteMeasure Î¼] : Î¼.InnerRegularWRT (IsCompact âˆ˜ closure) IsClosed := by
  refine innerRegular_of_univ Î¼ fun Îµ hÎµ â†¦ ?_
  by_cases h_top : Îµ = âˆ
  Â· rw [h_top]
    exact âŸ¨âˆ…, by rw [closure_empty]; exact isCompact_empty, measure_lt_top _ _âŸ©
  specialize hÎ¼ Îµ.toNNReal (ENNReal.toNNReal_pos hÎµ.ne' h_top)
  obtain âŸ¨K, hK_compact, hKâŸ© := hÎ¼
  rw [ENNReal.coe_toNNReal h_top] at hK
  exact âŸ¨K, hK_compact, hKâŸ©

theorem innerRegular_isCompact_isClosed_of_univ [TopologicalSpace Î±] [OpensMeasurableSpace Î±]
    (Î¼ : Measure Î±) (hÎ¼ : âˆ€ (Îµ : â„â‰¥0âˆ) (hÎµ : 0 < Îµ), âˆƒ K, IsCompact K âˆ§ IsClosed K âˆ§ Î¼ (Ká¶œ) < Îµ)
    [IsFiniteMeasure Î¼] : Î¼.InnerRegularWRT (fun s â†¦ IsCompact s âˆ§ IsClosed s) IsClosed := by
  intro A hA r hr
  obtain âŸ¨K, hK_compact, hK_closed, hKA, h_ltâŸ© :
      âˆƒ K, IsCompact K âˆ§ IsClosed K âˆ§ K âŠ† A âˆ§ Î¼ (A \ K) < Î¼ A - r := by
    obtain âŸ¨K', hK'_compact, hK'_closed, hK'_ltâŸ© := hÎ¼ (Î¼ A - r) (tsub_pos_of_lt hr)
    refine âŸ¨K' âˆ© A, ?_, hK'_closed.inter hA, âŸ¨inter_subset_right, ?_âŸ©âŸ©
    Â· exact hK'_compact.inter_right hA
    Â· refine (measure_mono fun x â†¦ ?_).trans_lt hK'_lt
      simp only [diff_inter_self_eq_diff, mem_diff, mem_compl_iff, and_imp, imp_self, imp_true_iff]
  refine âŸ¨K, hKA, âŸ¨hK_compact, hK_closedâŸ©, ?_âŸ©
  rw [measure_diff hKA _ (measure_ne_top Î¼ _)] at h_lt
  exact lt_of_tsub_lt_tsub_left h_lt
  exact IsClosed.measurableSet hK_closed

theorem innerRegular_isCompact_isClosed_of_univ' [TopologicalSpace Î±] [OpensMeasurableSpace Î±]
    (Î¼ : Measure Î±) (hÎ¼ : âˆ€ (Îµ : â„â‰¥0) (hÎµ : 0 < Îµ), âˆƒ K, IsCompact K âˆ§ IsClosed K âˆ§ Î¼ (Ká¶œ) < Îµ)
    [IsFiniteMeasure Î¼] : Î¼.InnerRegularWRT (fun s â†¦ IsCompact s âˆ§ IsClosed s) IsClosed := by
  refine innerRegular_isCompact_isClosed_of_univ Î¼ fun Îµ hÎµ â†¦ ?_
  by_cases h_top : Îµ = âˆ
  Â· rw [h_top]
    exact âŸ¨âˆ…, isCompact_empty, isClosed_empty, measure_lt_top _ _âŸ©
  specialize hÎ¼ Îµ.toNNReal (ENNReal.toNNReal_pos hÎµ.ne' h_top)
  obtain âŸ¨K, hK_compact, hKâŸ© := hÎ¼
  rw [ENNReal.coe_toNNReal h_top] at hK
  exact âŸ¨K, hK_compact, hKâŸ©

/-- Every measure on a compact space is regular with respect to relatively compact sets. -/
theorem innerRegular_isCompact_isClosed_of_compactSpace [TopologicalSpace Î±] [CompactSpace Î±]
    [OpensMeasurableSpace Î±] (P : Measure Î±) [IsFiniteMeasure P] :
    P.InnerRegularWRT (IsCompact âˆ˜ closure) IsClosed := by
  refine innerRegular_of_univ' P ?_
  refine fun Îµ hÎµ â†¦ âŸ¨univ, by rw [closure_univ]; exact isCompact_univ, ?_âŸ©
  simpa only [Set.compl_univ, MeasureTheory.measure_empty, ENNReal.coe_pos]

theorem Inter_iUnion_uniform_balls_measure (m : Measure Î±) (s' : â„• â†’ Set Î±) (V : â„• â†’ Set (Î± Ã— Î±)) :
    m ((UniformSpace.interUnionBalls s' V)á¶œ) â‰¤
      âˆ‘' n, m ((â‹ƒ x âˆˆ s' n, UniformSpace.ball x (Prod.swap â»Â¹' V n))á¶œ) := by
  rw [UniformSpace.interUnionBalls, Set.compl_iInter]
  apply measure_iUnion_le

theorem measure_Inter_iUnion_uniform_balls (Îµ : â„â‰¥0) (m : Measure Î±) (s' : â„• â†’ Set Î±)
    (V : â„• â†’ Set (Î± Ã— Î±)) (Î´ : â„• â†’ â„â‰¥0)
    (hÎ´1 : âˆ€ n, m ((â‹ƒ x âˆˆ s' n, UniformSpace.ball x (Prod.swap â»Â¹' V n))á¶œ) â‰¤ Î´ n) (hÎ´2 : Summable Î´)
    (hÎ´3 : âˆ‘' n, Î´ n < Îµ) : m ((UniformSpace.interUnionBalls s' V)á¶œ) < Îµ := by
  apply lt_of_le_of_lt (Inter_iUnion_uniform_balls_measure m s' V)
  have hÎ´3' : (âˆ‘' n, Î´ n : â„â‰¥0âˆ) < (Îµ : â„â‰¥0âˆ) := by
    rw [â† ENNReal.coe_tsum, ENNReal.coe_lt_coe]
    exacts [hÎ´3, hÎ´2]
  exact lt_of_le_of_lt (ENNReal.tsum_le_tsum fun n â†¦ hÎ´1 n) hÎ´3'

theorem inner_regular_isCompact_is_closed_of_complete_countable' [UniformSpace Î±] [CompleteSpace Î±]
    [SecondCountableTopology Î±] [(uniformity Î±).IsCountablyGenerated]
    [OpensMeasurableSpace Î±] (P : Measure Î±) [IsFiniteMeasure P] (Îµ : â„â‰¥0) (hÎµ : 0 < Îµ) :
    âˆƒ (K : _) (_ : IsCompact (closure K)), P (Ká¶œ) < Îµ := by
  classical
  cases isEmpty_or_nonempty Î±
  case inl =>
    refine âŸ¨âˆ…, by rw [closure_empty]; exact isCompact_empty, ?_âŸ©
    rw [â† Set.univ_eq_empty_iff.mpr]
    Â· simpa only [compl_univ, measure_empty, ENNReal.coe_pos] using hÎµ
    Â· assumption
  case inr =>
    rcases TopologicalSpace.exists_countable_dense Î± with âŸ¨s, hsc, hsdâŸ©
    obtain
    âŸ¨t : â„• â†’ Set (Î± Ã— Î±), hto : âˆ€ i, t i âˆˆ (uniformity Î±).sets âˆ§ IsOpen (t i) âˆ§ SymmetricRel (t i),
      h_basis : (uniformity Î±).HasAntitoneBasis tâŸ© :=
    (@uniformity_hasBasis_open_symmetric Î± _).exists_antitone_subbasis
    cases' (Set.countable_iff_exists_surjective (Dense.nonempty hsd)).1 hsc with f hf
    let f : â„• â†’ Î± â†’ Set Î± := fun n x â†¦ UniformSpace.ball x (t n)
    obtain h_univ : âˆ€ n, (â‹ƒ x âˆˆ s, f n x) = univ :=
      fun n â†¦ Dense.biUnion_uniformity_ball hsd (hto n).1
    have h3 : âˆ€ (n : â„•) (Îµ : â„â‰¥0âˆ) (_ : 0 < Îµ),
      âˆƒ (s' : Set Î±) (_ : s'.Finite) (_ : s' âŠ† s), P ((â‹ƒ x âˆˆ s', f n x)á¶œ) < Îµ := by
      intro n Îµ hÎµ
      simp_rw [compl_iUnion]
      let S : Set (Set Î±) := (fun t â†¦ (f n t)á¶œ) '' s
      have h_count : Countable S := by
        simp only [countable_coe_iff]
        exact hsc.image _
      have h_mea : âˆ€ s âˆˆ S, MeasurableSet s := by
        rintro u âŸ¨x, _, rflâŸ©
        simp only [MeasurableSet.compl_iff, UniformSpace.ball]
        apply measurable_prod_mk_left
        apply IsOpen.measurableSet
        exact (hto n).2.1
      have h_inter_empty : â‹‚â‚€ S = âˆ… := by
        rw [â† compl_compl âˆ…, compl_empty, â† h_univ n]
        simp only [S, sInter_image, compl_iUnion]
      rcases continuous_at_emptyset_inter P S h_count h_mea h_inter_empty hÎµ
        with âŸ¨S', S'1, S'2, S'3âŸ©
      obtain hs' := Function.subset_image_fintype S'2 S'1
      rcases hs' with âŸ¨s', s'sub, s'fin, s'imâŸ©
      use s', s'fin, s'sub
      apply lt_of_eq_of_lt _ S'3
      simp only [â† s'im, sInter_image]
    choose! s' s'fin _ s'bound using h3
    rcases NNReal.exists_seq_pos_summable_lt Îµ hÎµ with âŸ¨Î´, hÎ´1, hÎ´2, hÎ´3âŸ©
    have hÎ´1' : âˆ€ n, 0 < (Î´ n : â„â‰¥0âˆ) := fun n â†¦ ENNReal.coe_pos.2 (hÎ´1 n)
    let u : â„• â†’ Finset Î± := fun n â†¦ (s'fin n (Î´ n) (hÎ´1' n)).toFinset
    let A := UniformSpace.interUnionBalls (fun n â†¦ (u n : Set Î±)) (fun n â†¦ t n)
    refine âŸ¨A, UniformSpace.isCompact_closure_interUnionBalls h_basis.toHasBasis u, ?_âŸ©
    have hP : P (closure A)á¶œ â‰¤ P (Aá¶œ) := by
      apply measure_mono
      rw [â† Set.compl_subset_compl, compl_compl, compl_compl]
      exact subset_closure
    suffices h_meas_balls : P ((UniformSpace.interUnionBalls (fun n â†¦ â†‘(u n)) fun n â†¦ t n)á¶œ) < Îµ by
      simp only [A, coe_toFinset] at hP h_meas_balls âŠ¢
      exact h_meas_balls
    Â· refine measure_Inter_iUnion_uniform_balls Îµ P (fun n â†¦ â†‘(u n)) (fun n â†¦ t n) Î´
        (fun n â†¦ ?_) hÎ´2 hÎ´3
      obtain h' := le_of_lt ((fun n â†¦ (s'bound n) (Î´ n) (hÎ´1' n)) n)
      have h1 : âˆ€ x, x âˆˆ s' n (Î´ n) â†” x âˆˆ u n := by
        intro x
        simp only [u, Finite.mem_toFinset]
      obtain h'' : âˆ€ n, Prod.swap â»Â¹' t n = t n := fun n â†¦ SymmetricRel.eq (hto n).2.2
      simp_rw [Finset.mem_coe, â† h1, h'']
      exact h'

theorem exists_compact_measurable_set_measure_lt_of_complete_countable [UniformSpace Î±]
    [CompleteSpace Î±] [SecondCountableTopology Î±]
    [(uniformity Î±).IsCountablyGenerated] [OpensMeasurableSpace Î±] (P : Measure Î±)
    [IsFiniteMeasure P] (Îµ : â„â‰¥0) (hÎµ : 0 < Îµ) : âˆƒ K, IsCompact K âˆ§ IsClosed K âˆ§ P (Ká¶œ) < Îµ := by
  obtain âŸ¨K, hK, hPKâŸ© := inner_regular_isCompact_is_closed_of_complete_countable' P Îµ hÎµ
  refine âŸ¨closure K, hK, isClosed_closure, (measure_mono ?_).trans_lt hPKâŸ©
  exact compl_subset_compl.mpr subset_closure

theorem innerRegular_isCompact_isClosed_of_complete_countable [UniformSpace Î±] [CompleteSpace Î±]
    [SecondCountableTopology Î±] [(uniformity Î±).IsCountablyGenerated]
    [OpensMeasurableSpace Î±] (P : Measure Î±) [IsFiniteMeasure P] :
    P.InnerRegularWRT (fun s â†¦ IsCompact s âˆ§ IsClosed s) IsClosed :=
  innerRegular_isCompact_isClosed_of_univ' P
    (exists_compact_measurable_set_measure_lt_of_complete_countable P)

theorem innerRegular_isCompact_isClosed_isOpen_of_complete_countable [PseudoEMetricSpace Î±]
    [CompleteSpace Î±] [SecondCountableTopology Î±] [OpensMeasurableSpace Î±]
    (P : Measure Î±) [IsFiniteMeasure P] :
    P.InnerRegularWRT (fun s â†¦ IsCompact s âˆ§ IsClosed s) IsOpen :=
  (innerRegular_isCompact_isClosed_of_complete_countable P).trans
    (Measure.InnerRegularWRT.of_pseudoMetrizableSpace P)

theorem innerRegular_isCompact_isClosed_measurableSet_of_complete_countable [PseudoEMetricSpace Î±]
    [CompleteSpace Î±] [SecondCountableTopology Î±] [BorelSpace Î±] (P : Measure Î±)
    [IsFiniteMeasure P] : P.InnerRegularWRT (fun s â†¦ IsCompact s âˆ§ IsClosed s) MeasurableSet := by
  suffices P.InnerRegularWRT (fun s â†¦ IsCompact s âˆ§ IsClosed s) fun s â†¦ MeasurableSet s âˆ§ P s â‰  âˆ by
    convert this
    simp only [eq_iff_iff, iff_self_and]
    exact fun _ â†¦ measure_ne_top P _
  refine Measure.InnerRegularWRT.measurableSet_of_isOpen ?_ ?_
  Â· exact innerRegular_isCompact_isClosed_isOpen_of_complete_countable P
  Â· rintro s t âŸ¨hs_compact, hs_closedâŸ© ht_open
    rw [diff_eq]
    exact âŸ¨hs_compact.inter_right ht_open.isClosed_compl,
      hs_closed.inter (isClosed_compl_iff.mpr ht_open)âŸ©

-- now unused. But useful in general?
instance weaklyRegular_of_polishSpace [TopologicalSpace Î±] [PolishSpace Î±] [BorelSpace Î±]
    (Î¼ : Measure Î±) [IsFiniteMeasure Î¼] : Î¼.WeaklyRegular :=
  letI := upgradePolishSpace Î±
  MeasureTheory.Measure.WeaklyRegular.of_pseudoMetrizableSpace_of_isFiniteMeasure Î¼

/-- On a Polish space, any finite measure is regular with respect to compact and closed sets. -/
theorem PolishSpace.innerRegular_isCompact_measurableSet [TopologicalSpace Î±] [PolishSpace Î±]
    [BorelSpace Î±] (Î¼ : Measure Î±) [IsFiniteMeasure Î¼] :
    Î¼.InnerRegularWRT (fun s â†¦ IsCompact s âˆ§ IsClosed s) MeasurableSet := by
  letI := upgradePolishSpace Î±
  exact innerRegular_isCompact_isClosed_measurableSet_of_complete_countable Î¼

end MeasureTheory
