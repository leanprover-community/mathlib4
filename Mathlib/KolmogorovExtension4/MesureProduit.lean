import Mathlib.KolmogorovExtension4.IonescuTulceaFinset2

open MeasureTheory ProbabilityTheory Finset ENNReal Filter Topology Function

variable {X : â„• â†’ Type*} [âˆ€ n, MeasurableSpace (X n)]
variable (Î¼ : (n : â„•) â†’ Measure (X n)) [hÎ¼ : âˆ€ n, IsProbabilityMeasure (Î¼ n)]

noncomputable def prod_meas : Measure ((n : â„•) â†’ X n) :=
  Measure.snd ((Î¼ 0) âŠ—â‚˜
    (@ionescu_ker _ (ProbabilityMeasure.nonempty âŸ¨Î¼ 0, hÎ¼ 0âŸ©) _
      (fun n â†¦ kernel.const _ (Î¼ (n + 1))) _))

instance : IsProbabilityMeasure (prod_meas Î¼) := by
  rw [prod_meas]
  infer_instance

theorem kernel.comap_const {X Y Z : Type*} [MeasurableSpace X] [MeasurableSpace Y]
    [MeasurableSpace Z] (Î¼ : Measure Z) {f : X â†’ Y} (hf : Measurable f) :
    kernel.comap (kernel.const Y Î¼) f hf = kernel.const X Î¼ := by
  ext1 x
  rw [kernel.const_apply, kernel.comap_apply, kernel.const_apply]

theorem prod_ioc (n : â„•) (f : (Ioc 0 (n + 1)) â†’ â„â‰¥0âˆ) :
    (f âŸ¨n + 1, mem_Ioc.2 âŸ¨n.succ_pos, le_refl _âŸ©âŸ©) *
      (âˆ i : Ioc 0 n, f âŸ¨i.1, Ioc_subset_Ioc_right n.le_succ i.2âŸ©) =
    âˆ i : Ioc 0 (n + 1), f i := by
  let g : â„• â†’ â„â‰¥0âˆ := fun k â†¦ if hk : k âˆˆ Ioc 0 (n + 1) then f âŸ¨k, hkâŸ© else 1
  have h1 : âˆ i : Ioc 0 n, f âŸ¨i.1, Ioc_subset_Ioc_right n.le_succ i.2âŸ© =
      âˆ i : Ioc 0 n, g i := by
    refine Finset.prod_congr rfl ?_
    simp only [Finset.mem_univ, mem_Ioc, true_implies, Subtype.forall, g]
    rintro k âŸ¨hk1, hk2âŸ©
    rw [dif_pos âŸ¨hk1, hk2.trans n.le_succâŸ©]
  have h2 : âˆ i : Ioc 0 (n + 1), f i = âˆ i : Ioc 0 (n + 1), g i := by
    refine Finset.prod_congr rfl ?_
    simp only [Finset.mem_univ, mem_Ioc, Subtype.coe_eta, dite_eq_ite, true_implies, Subtype.forall,
      g]
    intro k hk
    simp [hk]
  rw [h1, h2, Finset.prod_coe_sort, Finset.prod_coe_sort]
  have : f âŸ¨n + 1, right_mem_Ioc.2 n.succ_posâŸ© = g (n + 1) := by simp [g]
  rw [this]
  exact Finset.mul_prod_Ico_eq_prod_Icc (Nat.le_add_left (0 + 1) n)

theorem er_succ_preimage_pi {n : â„•} (hn : 0 < n) (s : (i : Ioc 0 (n + 1)) â†’ Set (X i)) :
    er 0 n (n + 1) hn n.le_succ â»Â¹' Set.univ.pi s =
      (Set.univ.pi (fun i : Ioc 0 n â†¦ s âŸ¨i.1, Ioc_subset_Ioc_right n.le_succ i.2âŸ©)) Ã—Ë¢
        ((e n).symm â»Â¹' (s âŸ¨n + 1, mem_Ioc.2 âŸ¨n.succ_pos, le_refl (n + 1)âŸ©âŸ©)) := by
  ext p
  simp only [er, Nat.succ_eq_add_one, Nat.reduceAdd, MeasurableEquiv.coe_mk, Equiv.coe_fn_mk,
    Set.mem_preimage, Set.mem_pi, Set.mem_univ, true_implies, Subtype.forall, mem_Ioc, e,
    MeasurableEquiv.symm_mk, Equiv.coe_fn_symm_mk, Set.mem_prod]
  refine âŸ¨fun h â†¦ âŸ¨fun i âŸ¨hi1, hi2âŸ© â†¦ ?_, ?_âŸ©, fun âŸ¨h1, h2âŸ© i âŸ¨hi1, hi2âŸ© â†¦ ?_âŸ©
  Â· convert h i âŸ¨hi1, hi2.trans n.le_succâŸ©
    rw [dif_pos hi2]
  Â· convert h (n + 1) âŸ¨n.succ_pos, le_refl _âŸ©
    simp
  Â· split_ifs with h
    Â· exact h1 i âŸ¨hi1, hâŸ©
    Â· cases (by omega : i = n + 1)
      exact h2

example (a b : Prop) (h : a) : a âˆ§ b = b := by exact âŸ¨h, rflâŸ©

theorem kerNat_prod {N : â„•} (hN : 0 < N) :
    (kerNat (fun n â†¦ kernel.const _ (Î¼ (n + 1))) 0 N) =
      kernel.const _ (Measure.pi (fun i : Ioc 0 N â†¦ Î¼ i)) := by
  ext1 xâ‚€
  refine Nat.le_induction ?_ (fun n hn hind â†¦ ?_) N (Nat.succ_le.2 hN)
  Â· rw [kerNat_succ, kernel.const_apply]
    refine (Measure.pi_eq (fun s ms â†¦ ?_)).symm
    have : Subsingleton (Ioc 0 1) := by
      constructor
      rintro âŸ¨i, hiâŸ© âŸ¨j, hjâŸ©
      rw [mem_Ioc] at hi hj
      simp only [Subtype.mk.injEq]
      omega
    rw [Fintype.prod_subsingleton _ âŸ¨1, mem_Ioc.2 âŸ¨zero_lt_one, le_refl _âŸ©âŸ©,
      kernel.map_apply' _ (e 0).measurable, kernel.const_apply]
    Â· congr with x
      simp only [Nat.reduceAdd, e, MeasurableEquiv.coe_mk, Equiv.coe_fn_mk, Nat.succ_eq_add_one,
        Set.mem_preimage, Set.mem_pi, Set.mem_univ, true_implies, Subtype.forall,
        Nat.Ioc_succ_singleton, zero_add, mem_singleton, Nat.zero_eq]
      refine âŸ¨fun h â†¦ h 1 rfl, fun h i hi â†¦ ?_âŸ©
      cases hi
      exact h
    exact MeasurableSet.univ_pi ms
  Â· rw [kernel.const_apply]
    refine (Measure.pi_eq fun s ms â†¦ ?_).symm
    rw [kerNat_succ_right _ _ _ (Nat.succ_le.1 hn), kerNat_succ, compProd,
      dif_pos âŸ¨Nat.succ_le.1 hn, n.lt_succ_selfâŸ©,
      kernel.map_apply' _ _ _ (MeasurableSet.univ_pi ms)]
    rw [er_succ_preimage_pi (Nat.succ_le.1 hn), split, kernel.map_const, kernel.comap_const,
      kernel.compProd_apply]
    Â· simp only [kernel.const_apply, Nat.succ_eq_add_one, Set.mem_prod]
      have this b : (Î¼ (n + 1)).map (e n) {c | b âˆˆ
          (Set.univ.pi (fun i : Ioc 0 n â†¦ s âŸ¨i.1, Ioc_subset_Ioc_right n.le_succ i.2âŸ©)) âˆ§
            c âˆˆ (e n).symm â»Â¹' (s âŸ¨n + 1, mem_Ioc.2 âŸ¨n.succ_pos, le_refl (n + 1)âŸ©âŸ©)} =
          (Set.univ.pi (fun i : Ioc 0 n â†¦ s âŸ¨i.1, Ioc_subset_Ioc_right n.le_succ i.2âŸ©)).indicator
          (fun _ â†¦ (Î¼ (n + 1)) (s âŸ¨n + 1, mem_Ioc.2 âŸ¨n.succ_pos, le_refl _âŸ©âŸ©)) b := by
        simp only [Nat.succ_eq_add_one, Set.indicator, Pi.one_apply, mul_ite, mul_one, mul_zero]
        split_ifs with h <;> simp [h]
        Â· rw [Measure.map_apply (e n).measurable]
          Â· rfl
          Â· exact (e n).measurable_invFun (ms âŸ¨n + 1, right_mem_Ioc.2 n.succ_posâŸ©)
      simp_rw [this]
      rw [lintegral_indicator_const, hind, kernel.const_apply, Measure.pi_pi]
      Â· exact prod_ioc n (fun i â†¦ (Î¼ i) (s i))
      Â· exact MeasurableSet.univ_pi (fun i â†¦ ms âŸ¨i.1, Ioc_subset_Ioc_right n.le_succ i.2âŸ©)
    apply MeasurableSet.prod
    Â· exact MeasurableSet.univ_pi (fun i â†¦ ms âŸ¨i.1, Ioc_subset_Ioc_right n.le_succ i.2âŸ©)
    Â· exact (e n).measurable_invFun (ms âŸ¨n + 1, right_mem_Ioc.2 n.succ_posâŸ©)

theorem prod_noyau_proj (N : â„•) :
    my_ker (fun n â†¦ kernel.const _ (Î¼ (n + 1))) N =
      kernel.map ((kernel.deterministic id measurable_id) Ã—â‚–
          (kernel.const _ (Measure.pi (fun i : Ioc 0 N â†¦ Î¼ i))))
        (er' N) (er' N).measurable := by
  rcases eq_zero_or_pos N with hN | hN
  Â· cases hN
    rw [my_ker_zero]
    have : IsEmpty (Ioc 0 0) := by simp
    rw [Measure.pi_of_empty]
    ext x s ms
    rw [kernel.map_apply, kernel.map_apply, kernel.deterministic_apply, kernel.prod_apply,
      kernel.deterministic_apply, kernel.const_apply, Measure.dirac_prod_dirac,
      Measure.map_apply zer.measurable ms, Measure.map_apply (er' 0).measurable ms,
      Measure.dirac_apply' _ (zer.measurable ms), Measure.dirac_apply' _ ((er' 0).measurable ms)]
    apply indicator_eq_indicator
    simp only [id_eq, zer, MeasurableEquiv.coe_mk, Equiv.coe_fn_mk, mem_preimage, er']
    congrm (fun i â†¦ ?_) âˆˆ s
    simp [(mem_Iic_zero i.2).symm]
  Â· rw [my_ker_pos _ hN, kerNat_prod _ hN]
    rfl

variable {Î¹ : Type*} {Î± : Î¹ â†’ Type*}

theorem preimage_proj (I J : Finset Î¹) [âˆ€ i : Î¹, Decidable (i âˆˆ I)]
    (hIJ : I âŠ† J) (s : (i : I) â†’ Set (Î± i)) :
    (fun t : (âˆ€ j : J, Î± j) â†¦ fun i : I â†¦ t âŸ¨i, hIJ i.2âŸ©) â»Â¹' (Set.univ.pi s) =
    (@Set.univ J).pi (fun j â†¦ if h : j.1 âˆˆ I then s âŸ¨j.1, hâŸ© else Set.univ) := by
  ext x
  simp only [Set.mem_preimage, Set.mem_pi, Set.mem_univ, true_implies, Subtype.forall]
  refine âŸ¨fun h i hi â†¦ ?_, fun h i i_mem â†¦ by simpa [i_mem] using h i (hIJ i_mem)âŸ©
  split_ifs with i_mem
  Â· exact h i i_mem
  Â· trivial

variable {Y : Î¹ â†’ Type*} [âˆ€ i, MeasurableSpace (Y i)]
variable (Î½ : (i : Î¹) â†’ Measure (Y i)) [hÎ½ : âˆ€ i, IsProbabilityMeasure (Î½ i)]

/-- Consider a family of probability measures. You can take their products for any fimite
subfamily. This gives a projective family of measures, see `IsProjectiveMeasureFamily`. -/
theorem isProjectiveMeasureFamily_pi :
    IsProjectiveMeasureFamily (fun I : Finset Î¹ â†¦ (Measure.pi (fun i : I â†¦ Î½ i))) := by
  classical
  refine fun I J hJI â†¦ Measure.pi_eq (fun s ms â†¦ ?_)
  rw [Measure.map_apply (measurable_projâ‚‚' (Î± := Y) I J hJI) (MeasurableSet.univ_pi ms),
    preimage_proj J I hJI, Measure.pi_pi]
  let g := fun i â†¦ (Î½ i) (if hi : i âˆˆ J then s âŸ¨i, hiâŸ© else Set.univ)
  have h1 : (@Finset.univ I _).prod (fun i â†¦ g i) = (@Finset.univ I.toSet _).prod (fun i â†¦ g i) :=
    Finset.prod_congr rfl (by simp)
  have h2 : (@Finset.univ J _).prod (fun i â†¦ (Î½ i) (s i)) =
      (@Finset.univ J.toSet _).prod (fun i â†¦ g i) :=
    Finset.prod_congr rfl (by simp [g])
  rw [h1, h2, Finset.prod_set_coe, Finset.prod_set_coe,
    Finset.toFinset_coe, Finset.toFinset_coe,
    Finset.prod_subset hJI (fun _ h h' â†¦ by simp [g, h, h'])]

-- theorem kolContent_eq_measure_pi [Fintype Î¹] {s : Set ((i : Î¹) â†’ Y i)} (hs : MeasurableSet s) :
--     kolContent (isProjectiveMeasureFamily_pi Î½) s = Measure.pi Î½ s := by
--   have : s = cylinder Finset.univ s := by simp
--   rw [kolContent_congr (I := Finset.univ)]

theorem Measure.map_prod_snd {X Y Z : Type*} [MeasurableSpace X] [MeasurableSpace Y]
    [MeasurableSpace Z] (Î¼ : Measure X) [IsProbabilityMeasure Î¼] (Î½ : Measure Y) [SFinite Î½]
    (f : Y â†’ Z) :
    (Î¼.prod Î½).snd.map f = (Î¼.prod (Î½.map f)).snd := by
  rw [Measure.snd_prod, Measure.snd_prod]

theorem Measure.map_snd_compProd {X Y Z : Type*} [MeasurableSpace X] [MeasurableSpace Y]
    [MeasurableSpace Z] (Î¼ : Measure X) [IsProbabilityMeasure Î¼] (Îº : kernel X Y)
    [IsSFiniteKernel Îº] {f : Y â†’ Z} (hf : Measurable f) :
    (Î¼ âŠ—â‚˜ Îº).snd.map f = Measure.snd (Î¼ âŠ—â‚˜ (kernel.map Îº f hf)) := by
  ext s ms
  rw [Measure.map_apply hf ms, Measure.snd_apply (ms.preimage hf),
    Measure.compProd_apply (measurable_snd (hf ms)), Measure.snd_apply ms,
    Measure.compProd_apply (measurable_snd ms)]
  refine lintegral_congr fun x â†¦ ?_
  simp_rw [Set.preimage_preimage]
  rw [kernel.map_apply', Set.preimage_preimage]
  exact measurable_id ms

lemma indicator_const_mul {Î± : Type*} (s : Set Î±) (c : â„â‰¥0âˆ) (a : Î±) :
    (s.indicator 1 a) * c = s.indicator (fun _ â†¦ c) a := by
  simp [Set.indicator]

theorem prod_iic (n : â„•) (f : (Iic n) â†’ â„â‰¥0âˆ) :
    (âˆ i : Ioc 0 n, f âŸ¨i.1, Ioc_subset_Iic_self i.2âŸ©) * f âŸ¨0, mem_Iic.2 <| zero_le _âŸ© =
    âˆ i : Iic n, f i := by
  let g : â„• â†’ â„â‰¥0âˆ := fun k â†¦ if hk : k âˆˆ Iic n then f âŸ¨k, hkâŸ© else 1
  have h1 : âˆ i : Ioc 0 n, f âŸ¨i.1, Ioc_subset_Iic_self i.2âŸ© = âˆ i : Ioc 0 n, g i := by
    refine Finset.prod_congr rfl ?_
    simp only [Finset.mem_univ, mem_Ioc, true_implies, Subtype.forall, g]
    rintro k âŸ¨hk1, hk2âŸ©
    rw [dif_pos <| mem_Iic.2 hk2]
  have h2 : âˆ i : Iic n, f i = âˆ i : Iic n, g i := by
    refine Finset.prod_congr rfl ?_
    simp only [Finset.mem_univ, mem_Ioc, Subtype.coe_eta, dite_eq_ite, true_implies, Subtype.forall,
      g]
    intro k hk
    simp [hk]
  rw [h1, h2, Finset.prod_coe_sort, Finset.prod_coe_sort]
  have : f âŸ¨0, mem_Iic.2 <| zero_le _âŸ© = g 0 := by simp [g]
  rw [this]
  exact Finset.prod_Ioc_mul_eq_prod_Icc (zero_le n)

theorem projectiveLimit_prod_meas : IsProjectiveLimit (prod_meas Î¼)
    (fun I : Finset â„• â†¦ (Measure.pi (fun i : I â†¦ Î¼ i))) := by
  have := ProbabilityMeasure.nonempty âŸ¨Î¼ 0, hÎ¼ 0âŸ©
  intro I
  have sub : I âŠ† Finset.Iic (I.sup id) := fun i hi â†¦ Finset.mem_Iic.2 <| Finset.le_sup (f := id) hi
  have : Measure.pi (fun i : I â†¦ Î¼ i) =
      (Measure.pi (fun i : Iic (I.sup id) â†¦ Î¼ i)).map
        (fun x (i : I) â†¦ x âŸ¨i.1, sub i.2âŸ©) := by
    conv_lhs => change (fun I : Finset â„• â†¦ Measure.pi (fun i : I â†¦ Î¼ i)) I
    rw [isProjectiveMeasureFamily_pi Î¼ (Finset.Iic (I.sup id)) I sub]
  simp_rw [this]
  have : (fun (x : (n : â„•) â†’ X n) (i : I) â†¦ x i) =
      (fun x (i : I) â†¦ x âŸ¨i.1, Finset.mem_Iic.2 <| Finset.le_sup (f := id) i.2âŸ©) âˆ˜
      (fun x (i : Iic (I.sup id)) â†¦ x i) := by
    ext x i
    simp
  rw [this, â† Measure.map_map (measurable_projâ‚‚' _ _ sub) (measurable_proj' _)]
  congr
  rw [prod_meas, Measure.map_snd_compProd, ionescu_ker_proj, prod_noyau_proj]
  Â· refine (Measure.pi_eq fun s ms â†¦ ?_).symm
    have mpis := MeasurableSet.univ_pi ms
    rw [Measure.snd_apply mpis, Measure.compProd_apply (measurable_snd mpis)]
    refine Eq.trans (b := âˆ«â» xâ‚€, (s âŸ¨0, mem_Iic.2 <| zero_le _âŸ©).indicator 1 (id xâ‚€) *
      âˆ i : Ioc 0 (I.sup id), (Î¼ â†‘i) (s âŸ¨i.1, Ioc_subset_Iic_self i.2âŸ©) âˆ‚Î¼ 0) ?_ ?_
    Â· refine lintegral_congr fun xâ‚€ â†¦ ?_
      have this : (er' (I.sup id)) â»Â¹' (Prod.mk xâ‚€ â»Â¹' (Prod.snd â»Â¹' Set.univ.pi fun i â†¦ s i)) =
          s âŸ¨0, mem_Iic.2 <| zero_le _âŸ© Ã—Ë¢
            Set.univ.pi (fun i : Ioc 0 (I.sup id) â†¦ s âŸ¨i.1, Ioc_subset_Iic_self i.2âŸ©) := by
        ext x
        simp only [er', MeasurableEquiv.coe_mk, Equiv.coe_fn_mk, Set.mem_preimage, Set.mem_pi,
          Set.mem_univ, true_implies, Subtype.forall, mem_Iic, Set.mem_prod, mem_Ioc]
        refine âŸ¨fun h â†¦ âŸ¨h 0 (zero_le _), fun i âŸ¨hi1, hi2âŸ© â†¦ ?_âŸ©, fun âŸ¨h1, h2âŸ© i hi â†¦ ?_âŸ©
        Â· convert h i hi2
          simp [hi1.ne.symm]
        Â· split_ifs with h
          Â· cases h; exact h1
          Â· exact h2 i âŸ¨by omega, hiâŸ©
      rw [kernel.map_apply', this, kernel.prod_apply, Measure.prod_prod, kernel.deterministic_apply,
        Measure.dirac_apply', kernel.const_apply, Measure.pi_pi]
      Â· exact ms _
      Â· exact measurable_prod_mk_left (m := inferInstance) (measurable_snd mpis)
    Â· simp_rw [indicator_const_mul, id_eq]
      rw [lintegral_indicator_const]
      Â· exact prod_iic (I.sup id) (fun i â†¦ (Î¼ i) (s i))
      Â· exact ms _


theorem kolContent_eq_prod_meas {A : Set ((n : â„•) â†’ X n)} (hA : A âˆˆ cylinders X) :
    kolContent (isProjectiveMeasureFamily_pi Î¼) A = prod_meas Î¼ A := by
  obtain âŸ¨s, S, mS, A_eqâŸ© : âˆƒ s S, MeasurableSet S âˆ§ A = cylinder s S := by
    simpa [mem_cylinders] using hA
  rw [kolContent_congr _ hA A_eq mS, A_eq, cylinder, â† Measure.map_apply (measurable_proj' _) mS,
    projectiveLimit_prod_meas Î¼]

variable {X : Î¹ â†’ Type*} [hX : âˆ€ i, MeasurableSpace (X i)]
variable (Î¼ : (i : Î¹) â†’ Measure (X i)) [hÎ¼ : âˆ€ i, IsProbabilityMeasure (Î¼ i)]

lemma omg_ (s : Set Î¹) (x : (i : s) â†’ X i) (i j : s) (h : i = j) (h' : X i = X j) :
    cast h' (x i) = x j := by
  subst h
  rfl

lemma omg'_ (Î± Î² : Type _) (h : Î± = Î²) (a : Î±) (s : Set Î±) (h' : Set Î± = Set Î²) :
    (cast h a âˆˆ cast h' s) = (a âˆˆ s) := by
  subst h
  rfl

lemma HEq_meas {i j : Î¹} (hij : i = j) :
    HEq (inferInstance : MeasurableSpace (X i)) (inferInstance : MeasurableSpace (X j)) := by
  cases hij; rfl

/-- This theorem is used to prove the existence of the product measure: the `kolContent` of
a decresaing sequence of cylinders with empty intersection converges to $0$, in the case where
the measurable spaces are indexed by a countable type. This implies the $\sigma$-additivity of
`kolContent` (see `sigma_additive_addContent_of_tendsto_zero`),
which allows to extend it to the $\sigma$-algebra by CarathÃ©odory's theorem. -/
theorem secondLemma
    (Ï† : â„• â‰ƒ Î¹) {A : â„• â†’ Set ((i : Î¹) â†’ X i)} (A_mem : âˆ€ n, A n âˆˆ cylinders X)
    (A_anti : Antitone A) (A_inter : â‹‚ n, A n = âˆ…) :
    Tendsto (fun n â†¦ kolContent (isProjectiveMeasureFamily_pi Î¼) (A n)) atTop (ğ“ 0) := by
  have : âˆ€ i, Nonempty (X i) := by
    have := fun i â†¦ ProbabilityMeasure.nonempty âŸ¨Î¼ i, hÎ¼ iâŸ©
    infer_instance
  set Î¼_proj := isProjectiveMeasureFamily_pi Î¼
  let Î¼_proj' := isProjectiveMeasureFamily_pi (fun k : â„• â†¦ Î¼ (Ï† k))
  have A_cyl n : âˆƒ s S, MeasurableSet S âˆ§ A n = cylinder s S := by
    simpa [mem_cylinders] using A_mem n
  choose s S mS A_eq using A_cyl
  -- The goal of the proof is to apply the same result when the index set is `â„•`. To do so we
  -- have to pull back the sets `sâ‚™` and `Sâ‚™` using equivalences.
  let t n := (s n).preimage Ï† Ï†.injective.injOn
  have h i : X (Ï† (Ï†.symm i)) = X i := congrArg X (Ï†.apply_symm_apply i)
  have e n i (h : i âˆˆ s n) : Ï†.symm i âˆˆ t n := by simpa [t] using h
  have e' n k (h : k âˆˆ t n) : Ï† k âˆˆ s n := by simpa [t] using h
  -- The function `f` does the link between families indexed by `â„•` and those indexed by `Î¹`.
  -- Here we have to use `cast` because otherwhise we land in `X (Ï† (Ï†.symm i))`, which is not
  -- definitionally equal to X i.
  have meas_cast i : Measurable (cast (h i)) := by
    apply measurable_cast
    exact HEq_meas (by simp)
  let f : ((k : â„•) â†’ X (Ï† k)) â†’ (i : Î¹) â†’ X i := fun x i â†¦ cast (h i) (x (Ï†.symm i))
  -- `aux n` is an equivalence between `sâ‚™` ans `tâ‚™`, it will be used to link the two.
  let aux n : s n â‰ƒ t n :=
    { toFun := fun i â†¦ âŸ¨Ï†.symm i, e n i.1 i.2âŸ©
      invFun := fun k â†¦ âŸ¨Ï† k, e' n k.1 k.2âŸ©
      left_inv := fun _ â†¦ by simp
      right_inv := fun _ â†¦ by simp }
  -- `gâ‚™` is the equivalent of `f` for families indexed by `tâ‚™` and `sâ‚™`.
  let g n : ((k : t n) â†’ X (Ï† k)) â†’ (i : s n) â†’ X i :=
    fun x i â†¦ cast (h i) (x (aux n i))
  -- Transfering from `â„•` to `Î¹` and then projecting on `sâ‚™` is the same as first
  -- projecting on `uâ‚™` and then transfering to `Î¹`.
  have test n : (fun (x : (i : Î¹) â†’ X i) (i : s n) â†¦ x i) âˆ˜ f =
      (g n) âˆ˜ (fun (x : (k : â„•) â†’ X (Ï† k)) (k : t n) â†¦ x k) := by
    ext x
    simp [f, g, aux]
  -- Now fe define `Bâ‚™` and `Tâ‚™` as follows. `Bâ‚™` is a cylinder.
  let B n := f â»Â¹' (A n)
  let T n := (g n) â»Â¹' (S n)
  have B_eq n : B n = cylinder (t n) (T n) := by
    simp_rw [B, A_eq, cylinder, â† Set.preimage_comp, test n]
    rfl
  -- `gâ‚™` is measurable. We have to play with `Heq` to prove measurability of `cast`.
  have mg n : Measurable (g n) :=
    measurable_pi_lambda _ (fun i â†¦ (meas_cast _).comp <| measurable_pi_apply _)
  -- We deduce that `Tâ‚™` is measurable.
  have mT n : MeasurableSet (T n) := (mS n).preimage (mg n)
  -- The sequence `(Bâ‚™)` satisfies the hypotheses of `firstLemma`, we now have to prove that we can
  -- rewrite the goal in terms of `B`.
  have B_anti : Antitone B := fun m n hmn â†¦ Set.preimage_mono <| A_anti hmn
  have B_inter : â‹‚ n, B n = âˆ… := by
    simp_rw [B, â† Set.preimage_iInter, A_inter, Set.preimage_empty]
  have B_mem n : B n âˆˆ cylinders (fun k â†¦ X (Ï† k)) :=
    (mem_cylinders (B n)).2 âŸ¨t n, T n, mT n, B_eq nâŸ©
  -- Taking the preimage of a product indexed by `sâ‚™` by `gâ‚™` yields a product indexed by `uâ‚™`,
  -- again we have to play with `cast`.
  have imp n (u : (i : s n) â†’ Set (X i)) : (g n) â»Â¹' (Set.univ.pi u) =
      Set.univ.pi (fun k : t n â†¦ u ((aux n).symm k)) := by
    ext x
    simp only [Equiv.coe_fn_mk, Set.mem_preimage, Set.mem_pi, Set.mem_univ, true_implies,
      Subtype.forall, Equiv.coe_fn_symm_mk, g, aux]
    refine âŸ¨fun h' k hk â†¦ ?_, fun h' i hi â†¦ ?_âŸ©
    Â· convert h' (Ï† k) (e' n k hk)
      rw [@omg_ â„• (fun k â†¦ X (Ï† k)) (t n) x âŸ¨Ï†.symm (Ï† k), by simp [hk]âŸ© âŸ¨k, hkâŸ©]
      simp
    Â· convert h' (Ï†.symm i) (e n i hi)
      rw [â† @omg_ Î¹ (fun i â†¦ Set (X i)) (s n) u âŸ¨Ï† (Ï†.symm i), by simp [hi]âŸ© âŸ¨i, hiâŸ© (by simp) _,
        omg'_ (X (Ï† (Ï†.symm i))) (X i) (by simp) (x âŸ¨Ï†.symm i, e n i hiâŸ©)
          (u âŸ¨Ï† (Ï†.symm i), by simp [hi]âŸ©) (by simp)]
  -- The pushforward measure of the product measure of `(Î½_{Ï† k})_{k âˆˆ tâ‚™}` by `gâ‚™` is the
  -- product measre of `(âˆ¨áµ¢)_{i âˆˆ sâ‚™}`.
  have test' n : Measure.pi (fun i : s n â†¦ Î¼ i) =
      (Measure.pi (fun k : t n â†¦ Î¼ (Ï† k))).map (g n) := by
    refine Measure.pi_eq (fun x mx â†¦ ?_)
    rw [Measure.map_apply (mg n), imp n, Measure.pi_pi,
      Fintype.prod_equiv (aux n).symm _ (fun i â†¦ (Î¼ i) (x i))]
    Â· simp [aux]
    Â· exact MeasurableSet.pi Set.countable_univ (by simp [mx])
  -- This yields the desired result: the `kolContent` of `Aâ‚™` is the same as the one of `Bâ‚™`.
  have crucial n : kolContent Î¼_proj (A n) = kolContent Î¼_proj' (B n) := by
    simp_rw [fun n â†¦ kolContent_congr Î¼_proj
      (by rw [mem_cylinders]; exact âŸ¨s n, S n, mS n, A_eq nâŸ©) (A_eq n) (mS n),
      fun n â†¦ kolContent_congr Î¼_proj'
      (by rw [mem_cylinders]; exact âŸ¨t n, T n, mT n, B_eq nâŸ©) (B_eq n) (mT n), T, test' n]
    rw [Measure.map_apply (mg n) (mS n)]
  simp_rw [crucial, fun n â†¦ kolContent_eq_prod_meas (fun k â†¦ Î¼ (Ï† k)) (B_mem n),
    â† measure_empty (Î¼ := prod_meas (fun k â†¦ Î¼ (Ï† k))), â† B_inter]
  exact tendsto_measure_iInter (fun n â†¦ cylinders_measurableSet (B_mem n))
    B_anti âŸ¨0, measure_ne_top _ _âŸ©

/-- The `kolContent` of `cylinder I S` can be computed by integrating the indicator of
`cylinder I S` over the variables indexed by `I`. -/
theorem kolContent_eq_lmarginal [DecidableEq Î¹] [âˆ€ (S : Finset Î¹) i, Decidable (i âˆˆ S)]
    (I : Finset Î¹) {S : Set ((i : I) â†’ X i)} (mS : MeasurableSet S) (x : (i : Î¹) â†’ X i) :
    kolContent (isProjectiveMeasureFamily_pi Î¼) (cylinder I S) =
    (âˆ«â‹¯âˆ«â»_I, (cylinder I S).indicator 1 âˆ‚Î¼) x := by
  rw [kolContent_congr (isProjectiveMeasureFamily_pi Î¼)
      (by rw [mem_cylinders]; exact âŸ¨I, S, mS, rflâŸ©) rfl mS,
    â† lintegral_indicator_oneâ‚€ mS.nullMeasurableSet]
  refine lintegral_congr <| fun x â†¦ ?_
  by_cases hx : x âˆˆ S <;> simp [hx, Function.updateFinset]

/-- This theorem is used to prove the existence of the product measure: the `kolContent` of
a decresaing sequence of cylinders with empty intersection converges to $0$.
This implies the $\sigma$-additivity of
`kolContent` (see `sigma_additive_addContent_of_tendsto_zero`),
which allows to extend it to the $\sigma$-algebra by CarathÃ©odory's theorem. -/
theorem thirdLemma (A : â„• â†’ Set ((i : Î¹) â†’ X i)) (A_mem : âˆ€ n, A n âˆˆ cylinders X)
    (A_anti : Antitone A) (A_inter : â‹‚ n, A n = âˆ…) :
    Tendsto (fun n â†¦ kolContent (isProjectiveMeasureFamily_pi Î¼) (A n)) atTop (ğ“ 0) := by
  have : âˆ€ i, Nonempty (X i) := by
    have := fun i â†¦ ProbabilityMeasure.nonempty âŸ¨Î¼ i, hÎ¼ iâŸ©
    infer_instance
  set Î¼_proj := isProjectiveMeasureFamily_pi Î¼
  have A_cyl n : âˆƒ s S, MeasurableSet S âˆ§ A n = cylinder s S := by
    simpa only [mem_cylinders, exists_prop] using A_mem n
  choose s S mS A_eq using A_cyl
  -- The family `(Aâ‚™)` only depends on a countable set of coordinates, called `u`. Therefore our
  -- goal is to see it as a family indexed by this countable set,
  -- so that we can apply `secondLemma`. The proof is very similar to the previous one, except
  -- that the use of coercions avoids manipulating `cast`, as equalities will hold by `rfl`.
  let u := â‹ƒ n, (s n).toSet
  -- `tâ‚™` will be `sâ‚™` seen as a subset of `u`.
  let t : â„• â†’ Finset u := fun n â†¦ (s n).preimage Subtype.val Subtype.val_injective.injOn
  -- These are a few lemmas to move between `sâ‚™` and `tâ‚™`.
  have su n : (s n).toSet âŠ† u := Set.subset_iUnion (fun n â†¦ (s n).toSet) n
  have st n i (hi : i âˆˆ s n) : âŸ¨i, su n hiâŸ© âˆˆ t n := by simpa [t] using hi
  have ts n i (hi : i âˆˆ t n) : i.1 âˆˆ s n := by simpa [t] using hi
  -- This brings again `aux`.
  let aux : (n : â„•) â†’ (s n â‰ƒ t n) := fun n â†¦
    { toFun := fun i â†¦ âŸ¨âŸ¨i.1, su n i.2âŸ©, st n i i.2âŸ©
      invFun := fun i â†¦ âŸ¨i.1.1, ts n i i.2âŸ©
      left_inv := fun i â†¦ by simp
      right_inv := fun i â†¦ by simp }
  have h n (i : s n) : X (aux n i) = X i.1 := rfl
  have imp n (x : (i : s n) â†’ Set (X i)) : Set.univ.pi (fun i : t n â†¦ x ((aux n).invFun i)) =
      (fun x i â†¦ cast (h n i) (x (aux n i))) â»Â¹' Set.univ.pi x := by
    ext y
    simp only [Set.mem_pi, Set.mem_univ, true_implies, Subtype.forall, Set.mem_preimage]
    exact âŸ¨fun h i hi â†¦ h i (su n hi) (st n i hi), fun h i hi1 hi2 â†¦ h i (ts n âŸ¨i, hi1âŸ© hi2)âŸ©
  have meas n : Measurable (fun (x : (i : t n) â†’ X i) i â†¦ cast (h n i) (x (aux n i))) := by
    apply measurable_pi_lambda
    exact fun _ â†¦ measurable_pi_apply _
  have crucial n : Measure.pi (fun i : s n â†¦ Î¼ i) =
      (Measure.pi (fun i : t n â†¦ Î¼ i)).map (fun x i â†¦ cast (h n i) (x (aux n i))) := by
    refine Measure.pi_eq (fun x mx â†¦ ?_)
    rw [Measure.map_apply (meas n), â† imp n x, Measure.pi_pi, Fintype.prod_equiv (aux n)]
    Â· simp [aux]
    Â· exact MeasurableSet.pi Set.countable_univ (by simp [mx])
  let T : (n : â„•) â†’ Set ((i : t n) â†’ X i) :=
    fun n â†¦ (fun x i â†¦ cast (h n i) (x (aux n i))) â»Â¹' (S n)
  have mT n : MeasurableSet (T n) := by
    apply (mS n).preimage (meas n)
  let B : â„• â†’ Set (âˆ€ i : u, X i) := fun n â†¦ cylinder (t n) (T n)
  classical
  have B_eq n : B n = (fun x : (i : u) â†’ X i â†¦ fun i â†¦ if hi : i âˆˆ u
      then x âŸ¨i, hiâŸ© else Classical.ofNonempty) â»Â¹' (A n) := by
    ext x
    simp [B, T, -cast_eq]
    have this k : (fun i : s k â†¦ (fun j â†¦ if hj : j âˆˆ u then x âŸ¨j, hjâŸ©
        else Classical.ofNonempty) i.1) = fun i â†¦ cast (h k i) (x (aux k i)) := by
      ext i
      simp only [i.2, su k i.2, â†“reduceDite, cast_eq]
      rfl
    rw [â† this, â† mem_cylinder (s n) (S n) (fun j â†¦ if hj : j âˆˆ u then x âŸ¨j, hjâŸ©
        else Classical.ofNonempty), â† A_eq]
  have B_anti : Antitone B := by
    intro m n hmn
    simp_rw [B_eq]
    exact Set.preimage_mono <| A_anti hmn
  have B_inter : â‹‚ n, B n = âˆ… := by
    simp_rw [B_eq, â† Set.preimage_iInter, A_inter, Set.preimage_empty]
  let Î¼_proj' := isProjectiveMeasureFamily_pi (fun i : u â†¦ Î¼ i)
  have this n : kolContent Î¼_proj (A n) = kolContent Î¼_proj' (B n) := by
    simp_rw [fun n â†¦ kolContent_congr Î¼_proj
      (by rw [mem_cylinders]; exact âŸ¨s n, S n, mS n, A_eq nâŸ©) (A_eq n) (mS n),
      fun n â†¦ kolContent_congr Î¼_proj'
      (by rw [mem_cylinders]; exact âŸ¨t n, T n, mT n, rflâŸ©) rfl (mT n), T, crucial n]
    rw [Measure.map_apply (meas n) (mS n)]
  simp_rw [this]
  -- We now have two cases: if `u` is finite, then the result is simple because
  -- we have an actual measure.
  rcases finite_or_infinite u with (u_fin | u_inf)
  Â· have obv : (fun _ â†¦ 1 : ((i : u) â†’ X i) â†’ â„â‰¥0âˆ) = 1 := rfl
    have := Fintype.ofFinite u
    have concl n : kolContent Î¼_proj' (B n) =
        (Measure.pi (fun i : u â†¦ Î¼ i)) (cylinder (t n) (T n)) := by
      simp_rw [B, kolContent_eq_lmarginal (fun i : u â†¦ Î¼ i) (t n) (mT n) Classical.ofNonempty]
      rw [â† lmarginal_eq_of_disjoint_diff (Î¼ := (fun i : u â†¦ Î¼ i)) _
          (dependsOn_cylinder_indicator (T n))
          (t n).subset_univ, lmarginal_univ, â† obv, lintegral_indicator_const]
      Â· simp
      Â· exact @measurableSet_cylinder u (fun i : u â†¦ X i) _ (t n) (T n) (mT n)
      Â· rw [Finset.coe_univ, â† Set.compl_eq_univ_diff]
        exact disjoint_compl_right
      Â· rw [â† obv, measurable_indicator_const_iff 1]
        exact @measurableSet_cylinder u (fun i : u â†¦ X i) _ (t n) (T n) (mT n)
    simp_rw [concl, â† measure_empty (Î¼ := Measure.pi (fun i : u â†¦ Î¼ i)), â† B_inter]
    exact tendsto_measure_iInter (fun n â†¦ measurableSet_cylinder (t n) (T n) (mT n))
      B_anti âŸ¨0, measure_ne_top _ _âŸ©
  Â· -- If `u` is infinite, then we have an equivalence with `â„•` so we can apply `secondLemma`.
    have count_u : Countable u := Set.countable_iUnion (fun n â†¦ (s n).countable_toSet)
    obtain âŸ¨Ï†, -âŸ© := Classical.exists_true_of_nonempty (Î± := â„• â‰ƒ u) nonempty_equiv_of_countable
    refine secondLemma (fun i : u â†¦ Î¼ i) Ï† (fun n â†¦ ?_) B_anti B_inter
    simp only [mem_cylinders, exists_prop]
    exact âŸ¨t n, T n, mT n, rflâŸ©

/-- The `kolContent` associated to a family of probability measures is $\simga$-subadditive. -/
theorem kolContent_sigma_subadditive â¦ƒf : â„• â†’ Set ((i : Î¹) â†’ X i)â¦„ (hf : âˆ€ n, f n âˆˆ cylinders X)
    (hf_Union : (â‹ƒ n, f n) âˆˆ cylinders X) :
    kolContent (isProjectiveMeasureFamily_pi Î¼) (â‹ƒ n, f n) â‰¤
    âˆ‘' n, kolContent (isProjectiveMeasureFamily_pi Î¼) (f n) := by
  classical
  have : âˆ€ i, Nonempty (X i) := by
    have := fun i â†¦ ProbabilityMeasure.nonempty âŸ¨Î¼ i, hÎ¼ iâŸ©;
    infer_instance
  refine (kolContent (isProjectiveMeasureFamily_pi Î¼)).sigma_subadditive_of_sigma_additive
    setRing_cylinders (fun f hf hf_Union hf' â†¦ ?_) f hf hf_Union
  refine sigma_additive_addContent_of_tendsto_zero setRing_cylinders
    (kolContent (isProjectiveMeasureFamily_pi Î¼)) (fun h â†¦ ?_) ?_ hf hf_Union hf'
  Â· rcases (mem_cylinders _).1 h with âŸ¨s, S, mS, s_eqâŸ©
    rw [s_eq, kolContent_eq_lmarginal Î¼ (mS := mS) (x := Classical.ofNonempty)]
    refine ne_of_lt (lt_of_le_of_lt ?_ (by norm_num : (1 : â„â‰¥0âˆ) < âŠ¤))
    rw [â† lmarginal_const (Î¼ := Î¼) (s := s) 1 Classical.ofNonempty]
    apply lmarginal_mono
    intro x
    apply Set.indicator_le
    simp
  Â· intro s hs anti_s inter_s
    exact thirdLemma Î¼ s hs anti_s inter_s

/-- The product measure of an arbitrary family of probability measures. It is defined as the unique
extension of the function which gives to cylinders the measure given by the assiocated product
measure. -/
noncomputable def measure_produit : Measure ((i : Î¹) â†’ X i) := by
  exact Measure.ofAddContent setSemiringCylinders generateFrom_cylinders
    (kolContent (isProjectiveMeasureFamily_pi Î¼))
    (kolContent_sigma_subadditive Î¼)

/-- The product measure is the projective limit of the partial product measures. This ensures
uniqueness and expresses the value of the product measures applied to cylinders. -/
theorem isProjectiveLimit_measure_produit :
    IsProjectiveLimit (measure_produit Î¼) (fun I : Finset Î¹ â†¦ (Measure.pi (fun i : I â†¦ Î¼ i))) := by
  intro I
  ext1 s hs
  rw [Measure.map_apply _ hs]
  swap; Â· apply measurable_proj
  have h_mem : (fun (x : (i : Î¹) â†’ X i) (i : I) â†¦ x i) â»Â¹' s âˆˆ cylinders X := by
    rw [mem_cylinders]; exact âŸ¨I, s, hs, rflâŸ©
  rw [measure_produit, Measure.ofAddContent_eq _ _ _ _ h_mem,
    kolContent_congr (isProjectiveMeasureFamily_pi Î¼) h_mem rfl hs]

instance : IsProbabilityMeasure (measure_produit Î¼) := by
  constructor
  rw [â† cylinder_univ âˆ…, cylinder, â† Measure.map_apply, isProjectiveLimit_measure_produit Î¼]
  Â· simp
  Â· exact measurable_proj _
  Â· exact MeasurableSet.univ

theorem measure_boxes {s : Finset Î¹} {t : (i : Î¹) â†’ Set (X i)}
    (mt : âˆ€ i âˆˆ s, MeasurableSet (t i)) :
    measure_produit Î¼ (Set.pi s t) = âˆ i âˆˆ s, (Î¼ i) (t i) := by
  have : Set.pi s t = cylinder s ((@Set.univ s).pi (fun i : s â†¦ t i)) := by
    ext x
    simp
  rw [this, cylinder, â† Measure.map_apply, isProjectiveLimit_measure_produit Î¼,
    Measure.pi_pi]
  Â· rw [Finset.univ_eq_attach, Finset.prod_attach _ (fun i â†¦ (Î¼ i) (t i))]
  Â· exact measurable_proj _
  Â· exact MeasurableSet.pi Set.countable_univ fun i _ â†¦ mt i.1 i.2

theorem measure_cylinder {s : Finset Î¹} {S : Set ((i : s) â†’ X i)} (mS : MeasurableSet S) :
    measure_produit Î¼ (cylinder s S) = Measure.pi (fun i : s â†¦ Î¼ i) S := by
  rw [cylinder, â† Measure.map_apply (measurable_proj' _) mS, isProjectiveLimit_measure_produit Î¼]

theorem integral_dep_measure_prod {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    {s : Finset Î¹} {f : ((i : s) â†’ X i) â†’ E} (hf : StronglyMeasurable f) :
    âˆ« y, f ((fun x (i : s) â†¦ x i) y) âˆ‚measure_produit Î¼ =
    âˆ« y, f y âˆ‚Measure.pi (fun i : s â†¦ Î¼ i) := by
  rw [â† integral_map (measurable_proj' _).aemeasurable hf.aestronglyMeasurable,
    isProjectiveLimit_measure_produit Î¼]

theorem integral_dependsOn [DecidableEq Î¹] {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    {s : Finset Î¹} {f : ((i : Î¹) â†’ X i) â†’ E} (mf : StronglyMeasurable f) (hf : DependsOn f s)
    (x : (i : Î¹) â†’ X i) :
    âˆ« y, f y âˆ‚measure_produit Î¼ =
    âˆ« y, f (Function.updateFinset x s y) âˆ‚Measure.pi (fun i : s â†¦ Î¼ i) := by
  let g : ((i : s) â†’ X i) â†’ E := fun y â†¦ f (Function.updateFinset x _ y)
  have this y : g ((fun z (i : s) â†¦ z i) y) = f y := by
    apply hf
    intro i hi
    simp only [Function.updateFinset, dite_eq_ite, ite_eq_left_iff]
    exact fun h â†¦ (h hi).elim
  rw [â† integral_congr_ae <| eventually_of_forall this, integral_dep_measure_prod]
  exact mf.comp_measurable measurable_updateFinset

theorem lintegral_dep {s : Finset Î¹} {f : ((i : s) â†’ X i) â†’ â„â‰¥0âˆ} (hf : Measurable f) :
    âˆ«â» y, f ((fun x (i : s) â†¦ x i) y) âˆ‚measure_produit Î¼ =
    âˆ«â» y, f yâˆ‚Measure.pi (fun i : s â†¦ Î¼ i) := by
  rw [â† lintegral_map hf (measurable_proj' _), isProjectiveLimit_measure_produit Î¼]

theorem lintegral_dependsOn [DecidableEq Î¹]
    {f : ((i : Î¹) â†’ X i) â†’ â„â‰¥0âˆ} (mf : Measurable f) {s : Finset Î¹} (hf : DependsOn f s)
    (x : (i : Î¹) â†’ X i) : âˆ«â» y, f y âˆ‚measure_produit Î¼ = (âˆ«â‹¯âˆ«â»_s, f âˆ‚Î¼) x := by
  let g : ((i : s) â†’ X i) â†’ â„â‰¥0âˆ := fun y â†¦ f (Function.updateFinset x _ y)
  have this y : g ((fun z (i : s) â†¦ z i) y) = f y := by
    refine hf fun i hi â†¦ ?_
    simp only [Function.updateFinset, dite_eq_ite, ite_eq_left_iff]
    exact fun h â†¦ (h hi).elim
  simp_rw [â† this]
  rw [lintegral_dep]
  Â· rfl
  Â· exact mf.comp measurable_updateFinset
