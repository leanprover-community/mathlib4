import Mathlib.NumberTheory.NumberField.Cyclotomic.Ideal
import Mathlib.NumberTheory.NumberField.Ideal.KummerDedekind
import Mathlib.RingTheory.Polynomial.Cyclotomic.Factorization
import Mathlib.Misc

namespace IsCyclotomicExtension.Rat

open Ideal NumberField

section general

variable (n : â„•) [NeZero n] {K : Type*} [Field K] [NumberField K] [IsCyclotomicExtension {n} â„š K]
  (p k m : â„•) [hp : Fact (p.Prime)] (P : Ideal (ğ“ K)) [P.IsMaximal]
  [P.LiesOver (Ideal.span {(p : â„¤)})]

local notation3 "ğ’‘" => (Ideal.span {(p : â„¤)})

open NumberField RingOfIntegers Ideal IntermediateField

set_option maxHeartbeats 250000 in
theorem IsCyclotomicExtension.Rat.inertiaDeg_ramificationIdx (hn : n = p ^ (k + 1) * m)
    (hm : Â¬ p âˆ£ m) :
    inertiaDeg ğ’‘ P = orderOf (p : ZMod m) âˆ§
      ramificationIdx (algebraMap â„¤ (ğ“ K)) ğ’‘ P = p ^ k * (p - 1) := by
  classical
  have : IsAbelianGalois â„š K := IsCyclotomicExtension.isAbelianGalois {n} â„š K
  have : NeZero m := âŸ¨fun h â†¦ by simp [h] at hmâŸ©
  let Î¶ := zeta n â„š K
  have hÎ¶ := zeta_spec n â„š K
  -- Root of unity of order `m`
  let Î¶â‚˜ := Î¶ ^ (p ^ (k + 1))
  have hÎ¶â‚˜ := hÎ¶.pow (NeZero.pos _) hn
  -- Root of unity of order `p ^ (k + 1)`
  let Î¶â‚š := Î¶ ^ m
  have hÎ¶â‚š := hÎ¶.pow (NeZero.pos _) (mul_comm _ m â–¸ hn)
  let Fâ‚˜ := â„šâŸ®Î¶â‚˜âŸ¯
  have : IsCyclotomicExtension {m} â„š Fâ‚˜ :=
    (isCyclotomicExtension_singleton_iff_eq_adjoin _ _ _ _ hÎ¶â‚˜).mpr rfl
  let Fâ‚š := â„šâŸ®Î¶â‚šâŸ¯
  have : IsCyclotomicExtension {p ^ (k + 1)} â„š Fâ‚š :=
    (isCyclotomicExtension_singleton_iff_eq_adjoin _ _ _ _ hÎ¶â‚š).mpr rfl
  -- The prime ideal of `â„šâŸ®Î¶â‚˜âŸ¯` below `P`
  let Pâ‚˜ := comap (algebraMap (ğ“ Fâ‚˜) (ğ“ K)) P
  have : Pâ‚˜.IsMaximal := isMaximal_comap_of_isIntegral_of_isMaximal _
  -- The prime ideal of `â„šâŸ®Î¶â‚šâŸ¯` below `P`
  let Pâ‚š := comap (algebraMap (ğ“ Fâ‚š) (ğ“ K)) P
  have : Pâ‚š.IsMaximal := isMaximal_comap_of_isIntegral_of_isMaximal _
  have hâ‚ := ramificationIdx_algebra_tower (p := ğ’‘) (P := Pâ‚š) (Q := P)
    (by
      refine map_ne_bot_of_ne_bot ?_
      apply Ring.ne_bot_of_isMaximal_of_not_isField inferInstance (not_isField Fâ‚š))
    (by
      apply map_ne_bot_of_ne_bot
      simpa using hp.out.ne_zero)
    (by
      apply Ideal.map_comap_le)
  have hâ‚‚ := inertiaDeg_algebra_tower ğ’‘ Pâ‚˜ P
  have hâ‚ƒ : (ğ’‘.primesOver (ğ“ K)).ncard = (ğ’‘.primesOver (ğ“ Fâ‚˜)).ncard *
      (Pâ‚˜.primesOver (ğ“ K)).ncard := by
    rw [ncard_primesOver_eq_sum_ncard_primesOver â„¤ (ğ“ Fâ‚˜)]
    conv_lhs =>
      enter [2, P]
      rw [ncard_primesOver_eq_ncard_primesOver â„š Fâ‚˜ K ğ’‘ P.val Pâ‚˜]
    rw [Finset.sum_const, smul_eq_mul, Finset.card_univ]
    rw [â† Set.toFinset_card, â† Set.ncard_eq_toFinset_card']
  have h_main := ncard_primesOver_mul_ramificationIdxIn_mul_inertiaDegIn (p := ğ’‘)
    (by simpa using hp.out.ne_zero) (ğ“ K) â„š K
  rw [finrank n K, hn, Nat.totient_mul, Nat.totient_prime_pow, add_tsub_cancel_right] at h_main
  Â· rw [ramificationIdxIn_eq_ramificationIdx ğ’‘ P â„š K] at h_main
    rw [inertiaDegIn_eq_inertiaDeg ğ’‘ P â„š K] at h_main
    rw [hâ‚, hâ‚‚, hâ‚ƒ] at h_main
    rw [ramificationIdx_eq_of_prime_pow p k] at h_main
    rw [â† finrank m Fâ‚˜] at h_main
    rw [â† ncard_primesOver_mul_ramificationIdxIn_mul_inertiaDegIn (p := ğ’‘)
      (by simpa using hp.out.ne_zero) (ğ“ Fâ‚˜)
      â„š Fâ‚˜] at h_main
    rw [ramificationIdxIn_eq_ramificationIdx ğ’‘ Pâ‚˜ â„š Fâ‚˜] at h_main
    rw [inertiaDegIn_eq_inertiaDeg ğ’‘ Pâ‚˜ â„š Fâ‚˜] at h_main
    rw [ramificationIdx_of_not_dvd m, one_mul] at h_main
    Â· ring_nf at h_main
      simp_rw [mul_assoc] at h_main
      rw [Nat.mul_right_inj] at h_main
      Â· rw [mul_comm (ğ’‘.inertiaDeg Pâ‚˜)] at h_main
        simp_rw [â† mul_assoc] at h_main
        rw [Nat.mul_left_inj] at h_main
        Â· suffices (Pâ‚˜.primesOver (ğ“ K)).ncard * ramificationIdx (algebraMap (ğ“ Fâ‚š) (ğ“ K)) Pâ‚š P *
              Pâ‚˜.inertiaDeg P = 1 by
            rw [hâ‚, hâ‚‚]
            rw [Nat.eq_one_of_mul_eq_one_left this]
            rw [Nat.eq_one_of_mul_eq_one_left (Nat.eq_one_of_mul_eq_one_right this)]
            rw [mul_one, mul_one, inertiaDeg_of_not_dvd m, ramificationIdx_eq_of_prime_pow p k]
            Â· exact Nat.pair_eq_pair.mp rfl
            Â· exact hm
          rwa [mul_assoc _ (p ^ k), mul_comm (Pâ‚˜.primesOver (ğ“ K)).ncard, mul_assoc, mul_assoc,
            Nat.mul_eq_left, â† mul_assoc] at h_main
          Â· exact Nat.mul_ne_zero_iff.mpr âŸ¨NeZero.ne _, Nat.sub_ne_zero_iff_lt.mpr hp.out.one_ltâŸ©
        Â· exact inertiaDeg_ne_zero _ _
      Â· apply primesOver_ncard_ne_zero
    Â· exact hm
  Â· exact hp.out
  Â· exact Nat.zero_lt_succ k
  Â· refine Nat.Coprime.pow_left (k + 1) ?_
    exact not_not.mp <| (Nat.Prime.dvd_iff_not_coprime hp.out).not.mp hm



end general

end IsCyclotomicExtension.Rat





theorem IsCyclotomicExtension_single_iff_single_two_mul_of_odd (n : â„•) (hn : Odd n)
    (A B : Type*) [CommRing A] [CommRing B] [Nontrivial B] [NoZeroDivisors B] [Algebra A B]
    (hB : ringChar B â‰  2) :
    IsCyclotomicExtension {n} A B â†” IsCyclotomicExtension {2 * n} A B := by
  have : NeZero n := by
    refine âŸ¨?_âŸ©
    exact Nat.ne_of_odd_add hn
  have h : orderOf (-1 : B) = 2 := by
    rw [orderOf_neg_one, if_neg hB]
  rw [IsCyclotomicExtension.iff_singleton, IsCyclotomicExtension.iff_singleton]
  congr! 1
  Â· refine âŸ¨?_, ?_âŸ©
    Â· intro âŸ¨Î¶, hÎ¶âŸ©
      refine âŸ¨-Î¶, ?_âŸ©
      convert IsPrimitiveRoot.orderOf (-Î¶)
      rw [neg_eq_neg_one_mul, (Commute.all _ _).orderOf_mul_eq_mul_orderOf_of_coprime]
      Â· rw [h, hÎ¶.eq_orderOf]
      Â· rw [h, â† hÎ¶.eq_orderOf]
        exact Nat.coprime_two_left.mpr hn
    Â· intro âŸ¨Î¶, hÎ¶âŸ©
      exact âŸ¨Î¶ ^ 2, hÎ¶.pow (NeZero.pos _) rflâŸ©
  Â· suffices Algebra.adjoin A {b : B | b ^ n = 1} = Algebra.adjoin A {b : B | b ^ (2 * n) = 1} by
      rw [SetLike.ext_iff] at this
      exact forall_congr' this
    apply le_antisymm
    Â· apply Algebra.adjoin_mono
      intro b hb
      rw [Set.mem_setOf_eq, mul_comm, pow_mul, hb, one_pow]
    Â· apply Algebra.adjoin_le
      intro b hb
      rw [Set.mem_setOf_eq, mul_comm, pow_mul, sq_eq_one_iff] at hb
      obtain hb | hb := hb
      Â· apply Algebra.subset_adjoin
        exact hb
      Â· simp only [SetLike.mem_coe]
        rw [show b = - - b by exact Eq.symm (InvolutiveNeg.neg_neg b)]
        apply Subalgebra.neg_mem
        apply Algebra.subset_adjoin
        rw [Set.mem_setOf_eq, neg_pow, Odd.neg_one_pow hn, neg_mul, one_mul, hb, neg_neg]



-- Golf `IsCyclotomicExtension.of_union_of_dvd` using this
theorem IsCyclotomicExtension.exists_prim_root_of_dvd {n : â„•} [NeZero n] {S : Set â„•} (A B : Type*)
    [CommRing A] [CommRing B] [Algebra A B] (h : âˆƒ s âˆˆ S, s â‰  0 âˆ§ n âˆ£ s)
    [H : IsCyclotomicExtension S A B] :
    âˆƒ (r : B), IsPrimitiveRoot r n := by
  obtain âŸ¨m, hm, hm', âŸ¨x, rflâŸ©âŸ© := h
  obtain âŸ¨Î¶, hÎ¶âŸ© := H.exists_isPrimitiveRoot hm hm'
  refine âŸ¨Î¶ ^ x, ?_âŸ©
  have h_xnz : x â‰  0 := Nat.ne_zero_of_mul_ne_zero_right hm'
  have := hÎ¶.pow_of_dvd h_xnz (dvd_mul_left x n)
  rwa [mul_div_cancel_rightâ‚€ _ h_xnz] at this

open NumberField Units

theorem NumberField.Units.mem_torsion' (K : Type*) [Field K] [NumberField K]
    {x : (ğ“ K)Ë£} :
    x âˆˆ torsion K â†” IsOfFinOrder x := CommGroup.mem_torsion _ _

theorem NumberField.dvd_torsionOrder_of_isPrimitiveRoot {n : â„•} [NeZero n] {K : Type*} [Field K]
    [NumberField K] {Î¶ : K} (hÎ¶ : IsPrimitiveRoot Î¶ n) :
    n âˆ£ torsionOrder K := by
  rw [torsionOrder, Fintype.card_eq_nat_card]
  replace hÎ¶ := (hÎ¶.toInteger_isPrimitiveRoot).isUnit_unit (NeZero.ne n)
  have hÎ¶' := CommGroup.mem_torsion_of_isPrimitiveRoot n hÎ¶
  convert orderOf_dvd_natCard (âŸ¨_, hÎ¶'âŸ© : torsion K)
  rw [Subgroup.orderOf_mk]
  exact hÎ¶.eq_orderOf

theorem NumberField.Units.torsionOrder_eq_of_isCyclotomicExtension (n : â„•) [NeZero n] {K : Type*}
    [Field K] [NumberField K] [hK : IsCyclotomicExtension {n} â„š K] :
    torsionOrder K = if Even n then n else 2 * n := by
  have hÎ¶ := hK.zeta_spec
  obtain âŸ¨Î¼, hÎ¼âŸ© : âˆƒ Î¼ : torsion K, orderOf Î¼ = torsionOrder K := by
    rw [torsionOrder, Fintype.card_eq_nat_card]
    exact IsCyclic.exists_ofOrder_eq_natCard
  rw [â† IsPrimitiveRoot.iff_orderOf, â† IsPrimitiveRoot.coe_submonoidClass_iff,
    â† IsPrimitiveRoot.coe_units_iff] at hÎ¼
  replace hÎ¼ := hÎ¼.map_of_injective (FaithfulSMul.algebraMap_injective (ğ“ K) K)
  have h := IsPrimitiveRoot.pow_mul_pow_lcm hÎ¶ hÎ¼ (NeZero.ne _) (torsionOrder_ne_zero K)
  have : NeZero (n.lcm (torsionOrder K)) :=
    NeZero.of_pos <| Nat.lcm_pos_iff.mpr âŸ¨NeZero.pos n, torsionOrder_pos KâŸ©
  have : IsCyclotomicExtension {n.lcm (torsionOrder K)} â„š K := by
    have := hK.union_of_isPrimitiveRoot _ _ _ h
    rwa [Set.union_comm, â† IsCyclotomicExtension.iff_union_of_dvd] at this
    exact âŸ¨n.lcm (torsionOrder K), by simp, NeZero.ne _, Nat.dvd_lcm_left _ _âŸ©
  have hmain := (IsCyclotomicExtension.Rat.finrank n K).symm.trans <|
    (IsCyclotomicExtension.Rat.finrank (n.lcm (torsionOrder K)) K)
  obtain hn | hn := Nat.even_or_odd n
  Â· rw [if_pos hn]
    apply dvd_antisymm
    Â· have := Nat.eq_of_totient_eq_totient (Nat.dvd_lcm_left _ _) hn hmain
      rwa [eq_comm, Nat.lcm_eq_left_iff_dvd] at this
    Â· exact NumberField.dvd_torsionOrder_of_isPrimitiveRoot hÎ¶
  Â· rw [if_neg (Nat.not_even_iff_odd.mpr hn)]
    have := (Nat.eq_or_eq_of_totient_eq_totient (Nat.dvd_lcm_left _ _) hmain).resolve_left ?_
    Â· rw [this, eq_comm, Nat.lcm_eq_right_iff_dvd]
      exact NumberField.dvd_torsionOrder_of_isPrimitiveRoot hÎ¶
    Â· rw [eq_comm, Nat.lcm_eq_left_iff_dvd]
      intro h
      exact Nat.not_even_iff_odd.mpr (Odd.of_dvd_nat hn h) (even_torsionOrder K)
