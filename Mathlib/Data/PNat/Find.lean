/-
Copyright (c) 2022 Yakov Pechersky. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yakov Pechersky, Floris van Doorn
-/
import Mathlib.Data.PNat.Basic

#align_import data.pnat.find from "leanprover-community/mathlib"@"207cfac9fcd06138865b5d04f7091e46d9320432"

/-!
# Explicit least witnesses to existentials on positive natural numbers

Implemented via calling out to `Nat.find`.

-/


namespace PNat

variable {p q : â„•+ â†’ Prop} [DecidablePred p] [DecidablePred q] (h : âˆƒ n, p n)

instance decidablePredExistsNat : DecidablePred fun n' : â„• => âˆƒ (n : â„•+) (_ : n' = n), p n :=
  fun n' =>
  decidable_of_iff' (âˆƒ h : 0 < n', p âŸ¨n', hâŸ©) <|
    Subtype.exists.trans <| by
      simp_rw [mk_coe, @exists_comm (_ < _) (_ = _), exists_prop, exists_eq_left']
      -- ğŸ‰ no goals
#align pnat.decidable_pred_exists_nat PNat.decidablePredExistsNat

--include h

/-- The `PNat` version of `Nat.findX` -/
protected def findX : { n // p n âˆ§ âˆ€ m : â„•+, m < n â†’ Â¬p m } := by
  have : âˆƒ (n' : â„•) (n : â„•+) (_ : n' = n), p n := Exists.elim h fun n hn => âŸ¨n, n, rfl, hnâŸ©
  -- âŠ¢ { n // p n âˆ§ âˆ€ (m : â„•+), m < n â†’ Â¬p m }
  have n := Nat.findX this
  -- âŠ¢ { n // p n âˆ§ âˆ€ (m : â„•+), m < n â†’ Â¬p m }
  refine' âŸ¨âŸ¨n, _âŸ©, _, fun m hm pm => _âŸ©
  Â· obtain âŸ¨n', hn', -âŸ© := n.prop.1
    -- âŠ¢ 0 < â†‘n
    rw [hn']
    -- âŠ¢ 0 < â†‘n'
    exact n'.prop
    -- ğŸ‰ no goals
  Â· obtain âŸ¨n', hn', pn'âŸ© := n.prop.1
    -- âŠ¢ p { val := â†‘n, property := (_ : 0 < â†‘n) }
    simpa [hn', Subtype.coe_eta] using pn'
    -- ğŸ‰ no goals
  Â· exact n.prop.2 m hm âŸ¨m, rfl, pmâŸ©
    -- ğŸ‰ no goals
#align pnat.find_x PNat.findX

/-- If `p` is a (decidable) predicate on `â„•+` and `hp : âˆƒ (n : â„•+), p n` is a proof that
there exists some positive natural number satisfying `p`, then `PNat.find hp` is the
smallest positive natural number satisfying `p`. Note that `PNat.find` is protected,
meaning that you can't just write `find`, even if the `PNat` namespace is open.

The API for `PNat.find` is:

* `PNat.find_spec` is the proof that `PNat.find hp` satisfies `p`.
* `PNat.find_min` is the proof that if `m < PNat.find hp` then `m` does not satisfy `p`.
* `PNat.find_min'` is the proof that if `m` does satisfy `p` then `PNat.find hp â‰¤ m`.
-/
protected def find : â„•+ :=
  PNat.findX h
#align pnat.find PNat.find

protected theorem find_spec : p (PNat.find h) :=
  (PNat.findX h).prop.left
#align pnat.find_spec PNat.find_spec

protected theorem find_min : âˆ€ {m : â„•+}, m < PNat.find h â†’ Â¬p m :=
  @(PNat.findX h).prop.right
#align pnat.find_min PNat.find_min

protected theorem find_min' {m : â„•+} (hm : p m) : PNat.find h â‰¤ m :=
  le_of_not_lt fun l => PNat.find_min h l hm
#align pnat.find_min' PNat.find_min'

variable {n m : â„•+}

theorem find_eq_iff : PNat.find h = m â†” p m âˆ§ âˆ€ n < m, Â¬p n := by
  constructor
  -- âŠ¢ PNat.find h = m â†’ p m âˆ§ âˆ€ (n : â„•+), n < m â†’ Â¬p n
  Â· rintro rfl
    -- âŠ¢ p (PNat.find h) âˆ§ âˆ€ (n : â„•+), n < PNat.find h â†’ Â¬p n
    exact âŸ¨PNat.find_spec h, fun _ => PNat.find_min hâŸ©
    -- ğŸ‰ no goals
  Â· rintro âŸ¨hm, hltâŸ©
    -- âŠ¢ PNat.find h = m
    exact le_antisymm (PNat.find_min' h hm) (not_lt.1 <| imp_not_comm.1 (hlt _) <| PNat.find_spec h)
    -- ğŸ‰ no goals
#align pnat.find_eq_iff PNat.find_eq_iff

@[simp]
theorem find_lt_iff (n : â„•+) : PNat.find h < n â†” âˆƒ m < n, p m :=
  âŸ¨fun h2 => âŸ¨PNat.find h, h2, PNat.find_spec hâŸ©, fun âŸ¨_, hmn, hmâŸ© =>
    (PNat.find_min' h hm).trans_lt hmnâŸ©
#align pnat.find_lt_iff PNat.find_lt_iff

@[simp]
theorem find_le_iff (n : â„•+) : PNat.find h â‰¤ n â†” âˆƒ m â‰¤ n, p m := by
  simp only [exists_prop, â† lt_add_one_iff, find_lt_iff]
  -- ğŸ‰ no goals
#align pnat.find_le_iff PNat.find_le_iff

@[simp]
theorem le_find_iff (n : â„•+) : n â‰¤ PNat.find h â†” âˆ€ m < n, Â¬p m := by
  simp only [â† not_lt, find_lt_iff, not_exists, not_and]
  -- ğŸ‰ no goals
#align pnat.le_find_iff PNat.le_find_iff

@[simp]
theorem lt_find_iff (n : â„•+) : n < PNat.find h â†” âˆ€ m â‰¤ n, Â¬p m := by
  simp only [â† add_one_le_iff, le_find_iff, add_le_add_iff_right]
  -- ğŸ‰ no goals
#align pnat.lt_find_iff PNat.lt_find_iff

@[simp]
theorem find_eq_one : PNat.find h = 1 â†” p 1 := by simp [find_eq_iff]
                                                  -- ğŸ‰ no goals
#align pnat.find_eq_one PNat.find_eq_one

-- porting notes: deleted `@[simp]` to satisfy the linter because `le_find_iff` is more general
theorem one_le_find : 1 < PNat.find h â†” Â¬p 1 :=
  not_iff_not.mp <| by simp
                       -- ğŸ‰ no goals
#align pnat.one_le_find PNat.one_le_find

theorem find_mono (h : âˆ€ n, q n â†’ p n) {hp : âˆƒ n, p n} {hq : âˆƒ n, q n} :
    PNat.find hp â‰¤ PNat.find hq :=
  PNat.find_min' _ (h _ (PNat.find_spec hq))
#align pnat.find_mono PNat.find_mono

theorem find_le {h : âˆƒ n, p n} (hn : p n) : PNat.find h â‰¤ n :=
  (PNat.find_le_iff _ _).2 âŸ¨n, le_rfl, hnâŸ©
#align pnat.find_le PNat.find_le

theorem find_comp_succ (h : âˆƒ n, p n) (hâ‚‚ : âˆƒ n, p (n + 1)) (h1 : Â¬p 1) :
    PNat.find h = PNat.find hâ‚‚ + 1 := by
  refine' (find_eq_iff _).2 âŸ¨PNat.find_spec hâ‚‚, fun n => PNat.recOn n _ _âŸ©
  -- âŠ¢ 1 < PNat.find hâ‚‚ + 1 â†’ Â¬p 1
  Â· simp [h1]
    -- ğŸ‰ no goals
  intro m _ hm
  -- âŠ¢ Â¬p (m + 1)
  simp only [add_lt_add_iff_right, lt_find_iff] at hm
  -- âŠ¢ Â¬p (m + 1)
  exact hm _ le_rfl
  -- ğŸ‰ no goals
#align pnat.find_comp_succ PNat.find_comp_succ

end PNat
