/-
Copyright (c) 2017 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro
-/
import Mathlib.Data.Fintype.Card
import Mathlib.GroupTheory.Perm.Basic
import Mathlib.Tactic.Ring

#align_import data.fintype.perm from "leanprover-community/mathlib"@"509de852e1de55e1efa8eacfa11df0823f26f226"

/-!
# `Fintype` instances for `Equiv` and `Perm`

Main declarations:
* `permsOfFinset s`: The finset of permutations of the finset `s`.

-/

open Function

open Nat

universe u v

variable {Œ± Œ≤ Œ≥ : Type*}

open Finset Function List Equiv Equiv.Perm

variable [DecidableEq Œ±] [DecidableEq Œ≤]

/-- Given a list, produce a list of all permutations of its elements. -/
def permsOfList : List Œ± ‚Üí List (Perm Œ±)
  | [] => [1]
  | a :: l => permsOfList l ++ l.bind fun b => (permsOfList l).map fun f => Equiv.swap a b * f
#align perms_of_list permsOfList

theorem length_permsOfList : ‚àÄ l : List Œ±, length (permsOfList l) = l.length !
  | [] => rfl
  | a :: l => by
    rw [length_cons, Nat.factorial_succ]
    -- ‚ä¢ length (permsOfList (a :: l)) = (length l + 1) * (length l)!
    simp only [permsOfList, length_append, length_permsOfList, length_bind, comp,
     length_map, map_const', sum_replicate, smul_eq_mul, succ_mul]
    ring
    -- üéâ no goals
#align length_perms_of_list length_permsOfList

theorem mem_permsOfList_of_mem {l : List Œ±} {f : Perm Œ±} (h : ‚àÄ x, f x ‚â† x ‚Üí x ‚àà l) :
    f ‚àà permsOfList l := by
  induction l generalizing f with
  | nil =>
    -- Porting note: applied `not_mem_nil` because it is no longer true definitionally.
    simp only [not_mem_nil] at h
    exact List.mem_singleton.2 (Equiv.ext fun x => Decidable.by_contradiction <| h x)
  | cons a l IH =>
  by_cases hfa : f a = a
  ¬∑ refine' mem_append_left _ (IH fun x hx => mem_of_ne_of_mem _ (h x hx))
    rintro rfl
    exact hx hfa
  have hfa' : f (f a) ‚â† f a := mt (fun h => f.injective h) hfa
  have : ‚àÄ x : Œ±, (Equiv.swap a (f a) * f) x ‚â† x ‚Üí x ‚àà l := by
    intro x hx
    have hxa : x ‚â† a := by
      rintro rfl
      apply hx
      simp only [mul_apply, swap_apply_right]
    refine' List.mem_of_ne_of_mem hxa (h x fun h => _)
    simp only [mul_apply, swap_apply_def, mul_apply, Ne.def, apply_eq_iff_eq] at hx
    split_ifs at hx with h_1
    exacts [hxa (h.symm.trans h_1), hx h]
  suffices f ‚àà permsOfList l ‚à® ‚àÉ b ‚àà l, ‚àÉ g ‚àà permsOfList l, Equiv.swap a b * g = f by
    simpa only [permsOfList, exists_prop, List.mem_map, mem_append, List.mem_bind]
  refine' or_iff_not_imp_left.2 fun _hfl => ‚ü®f a, _, Equiv.swap a (f a) * f, IH this, _‚ü©
  ¬∑ exact mem_of_ne_of_mem hfa (h _ hfa')
  ¬∑ rw [‚Üê mul_assoc, mul_def (swap a (f a)) (swap a (f a)), swap_swap, ‚Üê Perm.one_def, one_mul]
#align mem_perms_of_list_of_mem mem_permsOfList_of_mem

theorem mem_of_mem_permsOfList :
    -- porting notes: was `‚àÄ {x}` but need to capture the `x`
    ‚àÄ {l : List Œ±} {f : Perm Œ±}, f ‚àà permsOfList l ‚Üí (x :Œ± ) ‚Üí f x ‚â† x ‚Üí x ‚àà l
  | [], f, h, heq_iff_eq => by
    have : f = 1 := by simpa [permsOfList] using h
    -- ‚ä¢ ‚Üëf heq_iff_eq ‚â† heq_iff_eq ‚Üí heq_iff_eq ‚àà []
    rw [this]; simp
    -- ‚ä¢ ‚Üë1 heq_iff_eq ‚â† heq_iff_eq ‚Üí heq_iff_eq ‚àà []
               -- üéâ no goals
  | a :: l, f, h, x =>
    (mem_append.1 h).elim (fun h hx => mem_cons_of_mem _ (mem_of_mem_permsOfList h x hx))
      fun h hx =>
      let ‚ü®y, hy, hy'‚ü© := List.mem_bind.1 h
      let ‚ü®g, hg‚ÇÅ, hg‚ÇÇ‚ü© := List.mem_map.1 hy'
      -- Porting note: Seems like the implicit variable `x` of type `Œ±` is needed.
      if hxa : x = a then by simp [hxa]
                             -- üéâ no goals
      else
        if hxy : x = y then mem_cons_of_mem _ <| by rwa [hxy]
                                                    -- üéâ no goals
        else mem_cons_of_mem a <| mem_of_mem_permsOfList hg‚ÇÅ _ <| by
              rw [eq_inv_mul_iff_mul_eq.2 hg‚ÇÇ, mul_apply, swap_inv, swap_apply_def]
              -- ‚ä¢ (if ‚Üëf x = a then y else if ‚Üëf x = y then a else ‚Üëf x) ‚â† x
              split_ifs <;> [exact Ne.symm hxy; exact Ne.symm hxa; exact hx]
              -- üéâ no goals
#align mem_of_mem_perms_of_list mem_of_mem_permsOfList

theorem mem_permsOfList_iff {l : List Œ±} {f : Perm Œ±} :
    f ‚àà permsOfList l ‚Üî ‚àÄ {x}, f x ‚â† x ‚Üí x ‚àà l :=
  ‚ü®mem_of_mem_permsOfList, mem_permsOfList_of_mem‚ü©
#align mem_perms_of_list_iff mem_permsOfList_iff

theorem nodup_permsOfList : ‚àÄ {l : List Œ±} (_ : l.Nodup), (permsOfList l).Nodup
  | [], _ => by simp [permsOfList]
                -- üéâ no goals
  | a :: l, hl => by
    have hl' : l.Nodup := hl.of_cons
    -- ‚ä¢ Nodup (permsOfList (a :: l))
    have hln' : (permsOfList l).Nodup := nodup_permsOfList hl'
    -- ‚ä¢ Nodup (permsOfList (a :: l))
    have hmeml : ‚àÄ {f : Perm Œ±}, f ‚àà permsOfList l ‚Üí f a = a := fun {f} hf =>
      not_not.1 (mt (mem_of_mem_permsOfList hf _) (nodup_cons.1 hl).1)
    rw [permsOfList, List.nodup_append, List.nodup_bind, pairwise_iff_get]
    -- ‚ä¢ Nodup (permsOfList l) ‚àß ((‚àÄ (x : Œ±), x ‚àà l ‚Üí Nodup (List.map (fun f => Equiv ‚Ä¶
    refine ‚ü®?_, ‚ü®‚ü®?_,?_ ‚ü©, ?_‚ü©‚ü©
    ¬∑ exact hln'
      -- üéâ no goals
    ¬∑ exact fun _ _ => hln'.map fun _ _ => mul_left_cancel
      -- üéâ no goals
    ¬∑ intros i j hij x hx‚ÇÅ hx‚ÇÇ
      -- ‚ä¢ False
      let ‚ü®f, hf‚ü© := List.mem_map.1 hx‚ÇÅ
      -- ‚ä¢ False
      let ‚ü®g, hg‚ü© := List.mem_map.1 hx‚ÇÇ
      -- ‚ä¢ False
      have hix : x a = List.get l i := by
        rw [‚Üê hf.2, mul_apply, hmeml hf.1, swap_apply_left]
      have hiy : x a = List.get l j := by
        rw [‚Üê hg.2, mul_apply, hmeml hg.1, swap_apply_left]
      have hieqj : i = j := nodup_iff_injective_get.1 hl' (hix.symm.trans hiy)
      -- ‚ä¢ False
      exact absurd hieqj (_root_.ne_of_lt hij)
      -- üéâ no goals
    ¬∑ intros f hf‚ÇÅ hf‚ÇÇ
      -- ‚ä¢ False
      let ‚ü®x, hx, hx'‚ü© := List.mem_bind.1 hf‚ÇÇ
      -- ‚ä¢ False
      let ‚ü®g, hg‚ü© := List.mem_map.1 hx'
      -- ‚ä¢ False
      have hgxa : g‚Åª¬π x = a := f.injective <| by rw [hmeml hf‚ÇÅ, ‚Üê hg.2]; simp
      -- ‚ä¢ False
      have hxa : x ‚â† a := fun h => (List.nodup_cons.1 hl).1 (h ‚ñ∏ hx)
      -- ‚ä¢ False
      exact (List.nodup_cons.1 hl).1 <|
          hgxa ‚ñ∏ mem_of_mem_permsOfList hg.1 _ (by rwa [apply_inv_self, hgxa])
#align nodup_perms_of_list nodup_permsOfList

/-- Given a finset, produce the finset of all permutations of its elements. -/
def permsOfFinset (s : Finset Œ±) : Finset (Perm Œ±) :=
  Quotient.hrecOn s.1 (fun l hl => ‚ü®permsOfList l, nodup_permsOfList hl‚ü©)
    (fun a b hab =>
      hfunext (congr_arg _ (Quotient.sound hab)) fun ha hb _ =>
        heq_of_eq <| Finset.ext <| by simp [mem_permsOfList_iff, hab.mem_iff])
                                      -- üéâ no goals
    s.2
#align perms_of_finset permsOfFinset

theorem mem_perms_of_finset_iff :
    ‚àÄ {s : Finset Œ±} {f : Perm Œ±}, f ‚àà permsOfFinset s ‚Üî ‚àÄ {x}, f x ‚â† x ‚Üí x ‚àà s := by
  rintro ‚ü®‚ü®l‚ü©, hs‚ü© f; exact mem_permsOfList_iff
  -- ‚ä¢ f ‚àà permsOfFinset { val := Quot.mk Setoid.r l, nodup := hs } ‚Üî ‚àÄ {x : Œ±}, ‚Üëf ‚Ä¶
                      -- üéâ no goals
#align mem_perms_of_finset_iff mem_perms_of_finset_iff

theorem card_perms_of_finset : ‚àÄ s : Finset Œ±, (permsOfFinset s).card = s.card ! := by
  rintro ‚ü®‚ü®l‚ü©, hs‚ü©; exact length_permsOfList l
  -- ‚ä¢ card (permsOfFinset { val := Quot.mk Setoid.r l, nodup := hs }) = (card { va ‚Ä¶
                    -- üéâ no goals
#align card_perms_of_finset card_perms_of_finset

/-- The collection of permutations of a fintype is a fintype. -/
def fintypePerm [Fintype Œ±] : Fintype (Perm Œ±) :=
  ‚ü®permsOfFinset (@Finset.univ Œ± _), by simp [mem_perms_of_finset_iff]‚ü©
                                        -- üéâ no goals
#align fintype_perm fintypePerm

instance equivFintype [Fintype Œ±] [Fintype Œ≤] : Fintype (Œ± ‚âÉ Œ≤) :=
  if h : Fintype.card Œ≤ = Fintype.card Œ± then
    Trunc.recOnSubsingleton (Fintype.truncEquivFin Œ±) fun eŒ± =>
      Trunc.recOnSubsingleton (Fintype.truncEquivFin Œ≤) fun eŒ≤ =>
        @Fintype.ofEquiv _ (Perm Œ±) fintypePerm
          (equivCongr (Equiv.refl Œ±) (eŒ±.trans (Eq.recOn h eŒ≤.symm)) : Œ± ‚âÉ Œ± ‚âÉ (Œ± ‚âÉ Œ≤))
  else ‚ü®‚àÖ, fun x => False.elim (h (Fintype.card_eq.2 ‚ü®x.symm‚ü©))‚ü©

theorem Fintype.card_perm [Fintype Œ±] : Fintype.card (Perm Œ±) = (Fintype.card Œ±)! :=
  Subsingleton.elim (@fintypePerm Œ± _ _) (@equivFintype Œ± Œ± _ _ _ _) ‚ñ∏ card_perms_of_finset _
#align fintype.card_perm Fintype.card_perm

theorem Fintype.card_equiv [Fintype Œ±] [Fintype Œ≤] (e : Œ± ‚âÉ Œ≤) :
    Fintype.card (Œ± ‚âÉ Œ≤) = (Fintype.card Œ±)! :=
  Fintype.card_congr (equivCongr (Equiv.refl Œ±) e) ‚ñ∏ Fintype.card_perm
#align fintype.card_equiv Fintype.card_equiv
