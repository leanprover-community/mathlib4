/-
Copyright (c) 2014 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro
-/
import Mathlib.Algebra.CharZero.Defs
import Mathlib.Algebra.GroupWithZero.Commute
import Mathlib.Algebra.Hom.Ring
import Mathlib.Algebra.Order.Group.Abs
import Mathlib.Algebra.Ring.Commute
import Mathlib.Data.Nat.Order.Basic
import Mathlib.Algebra.Group.Opposite

#align_import data.nat.cast.basic from "leanprover-community/mathlib"@"acebd8d49928f6ed8920e502a6c90674e75bd441"

/-!
# Cast of natural numbers (additional theorems)

This file proves additional properties about the *canonical* homomorphism from
the natural numbers into an additive monoid with a one (`Nat.cast`).

## Main declarations

* `castAddMonoidHom`: `cast` bundled as an `AddMonoidHom`.
* `castRingHom`: `cast` bundled as a `RingHom`.
-/

-- Porting note: There are many occasions below where we need `simp [map_zero f]`
-- where `simp [map_zero]` should suffice. (Similarly for `map_one`.)
-- See https://leanprover.zulipchat.com/#narrow/stream/287929-mathlib4/topic/simp.20regression.20with.20MonoidHomClass

variable {Î± Î² : Type*}

namespace Nat

/-- `Nat.cast : â„• â†’ Î±` as an `AddMonoidHom`. -/
def castAddMonoidHom (Î± : Type*) [AddMonoidWithOne Î±] :
    â„• â†’+ Î± where
  toFun := Nat.cast
  map_add' := cast_add
  map_zero' := cast_zero
#align nat.cast_add_monoid_hom Nat.castAddMonoidHom

@[simp]
theorem coe_castAddMonoidHom [AddMonoidWithOne Î±] : (castAddMonoidHom Î± : â„• â†’ Î±) = Nat.cast :=
  rfl
#align nat.coe_cast_add_monoid_hom Nat.coe_castAddMonoidHom

@[simp, norm_cast]
theorem cast_mul [NonAssocSemiring Î±] (m n : â„•) : ((m * n : â„•) : Î±) = m * n := by
  induction n <;> simp [mul_succ, mul_add, *]
  -- âŠ¢ â†‘(m * zero) = â†‘m * â†‘zero
                  -- ðŸŽ‰ no goals
                  -- ðŸŽ‰ no goals
#align nat.cast_mul Nat.cast_mul

/-- `Nat.cast : â„• â†’ Î±` as a `RingHom` -/
def castRingHom (Î± : Type*) [NonAssocSemiring Î±] : â„• â†’+* Î± :=
  { castAddMonoidHom Î± with toFun := Nat.cast, map_one' := cast_one, map_mul' := cast_mul }
#align nat.cast_ring_hom Nat.castRingHom

@[simp]
theorem coe_castRingHom [NonAssocSemiring Î±] : (castRingHom Î± : â„• â†’ Î±) = Nat.cast :=
  rfl
#align nat.coe_cast_ring_hom Nat.coe_castRingHom

theorem cast_commute [NonAssocSemiring Î±] (n : â„•) (x : Î±) : Commute (n : Î±) x := by
  induction n with
  | zero => rw [Nat.cast_zero]; exact Commute.zero_left x
  | succ n ihn => rw [Nat.cast_succ]; exact ihn.add_left (Commute.one_left x)
#align nat.cast_commute Nat.cast_commute

theorem _root_.Commute.ofNat_left [NonAssocSemiring Î±] (n : â„•) [n.AtLeastTwo] (x : Î±) :
    Commute (OfNat.ofNat n) x :=
  n.cast_commute x

theorem cast_comm [NonAssocSemiring Î±] (n : â„•) (x : Î±) : (n : Î±) * x = x * n :=
  (cast_commute n x).eq
#align nat.cast_comm Nat.cast_comm

theorem commute_cast [NonAssocSemiring Î±] (x : Î±) (n : â„•) : Commute x n :=
  (n.cast_commute x).symm
#align nat.commute_cast Nat.commute_cast

theorem _root_.Commute.ofNat_right [NonAssocSemiring Î±] (x : Î±) (n : â„•) [n.AtLeastTwo] :
    Commute x (OfNat.ofNat n) :=
  n.commute_cast x

section OrderedSemiring
/- Note: even though the section indicates `OrderedSemiring`, which is the common use case,
we use a generic collection of instances so that it applies in other settings (e.g., in a
`StarOrderedRing`, or the `selfAdjoint` or `StarOrderedRing.positive` parts thereof). -/

variable [AddCommMonoidWithOne Î±] [PartialOrder Î±]
variable [CovariantClass Î± Î± (Â· + Â·) (Â· â‰¤ Â·)] [ZeroLEOneClass Î±]

@[mono]
theorem mono_cast : Monotone (Nat.cast : â„• â†’ Î±) :=
  monotone_nat_of_le_succ fun n â†¦ by
    rw [Nat.cast_succ]; exact le_add_of_nonneg_right zero_le_one
    -- âŠ¢ â†‘n â‰¤ â†‘n + 1
                        -- ðŸŽ‰ no goals
#align nat.mono_cast Nat.mono_cast

@[simp low]
theorem cast_nonneg' (n : â„•) : 0 â‰¤ (n : Î±) :=
  @Nat.cast_zero Î± _ â–¸ mono_cast (Nat.zero_le n)

-- without this more specific version Lean often chokes
@[simp]
theorem cast_nonneg {Î±} [OrderedSemiring Î±] (n : â„•) : 0 â‰¤ (n : Î±) :=
  cast_nonneg' n
#align nat.cast_nonneg Nat.cast_nonneg

section Nontrivial

variable [NeZero (1 : Î±)]

theorem cast_add_one_pos (n : â„•) : 0 < (n : Î±) + 1 :=
  zero_lt_one.trans_le <| le_add_of_nonneg_left n.cast_nonneg'
#align nat.cast_add_one_pos Nat.cast_add_one_pos

@[simp low]
theorem cast_pos' {n : â„•} : (0 : Î±) < n â†” 0 < n := by cases n <;> simp [cast_add_one_pos]
                                                      -- âŠ¢ 0 < â†‘zero â†” 0 < zero
                                                                  -- ðŸŽ‰ no goals
                                                                  -- ðŸŽ‰ no goals

-- without this more specific version Lean often chokes
@[simp]
theorem cast_pos {Î±} [OrderedSemiring Î±] [Nontrivial Î±] {n : â„•} : (0 : Î±) < n â†” 0 < n := cast_pos'
#align nat.cast_pos Nat.cast_pos

end Nontrivial

variable [CharZero Î±] {m n : â„•}

theorem strictMono_cast : StrictMono (Nat.cast : â„• â†’ Î±) :=
  mono_cast.strictMono_of_injective cast_injective
#align nat.strict_mono_cast Nat.strictMono_cast

/-- `Nat.cast : â„• â†’ Î±` as an `OrderEmbedding` -/
@[simps! (config := { fullyApplied := false })]
def castOrderEmbedding : â„• â†ªo Î± :=
  OrderEmbedding.ofStrictMono Nat.cast Nat.strictMono_cast
#align nat.cast_order_embedding Nat.castOrderEmbedding
#align nat.cast_order_embedding_apply Nat.castOrderEmbedding_apply

@[simp, norm_cast]
theorem cast_le : (m : Î±) â‰¤ n â†” m â‰¤ n :=
  strictMono_cast.le_iff_le
#align nat.cast_le Nat.cast_le

@[simp, norm_cast, mono]
theorem cast_lt : (m : Î±) < n â†” m < n :=
  strictMono_cast.lt_iff_lt
#align nat.cast_lt Nat.cast_lt

@[simp, norm_cast]
theorem one_lt_cast : 1 < (n : Î±) â†” 1 < n := by rw [â† cast_one, cast_lt]
                                                -- ðŸŽ‰ no goals
#align nat.one_lt_cast Nat.one_lt_cast

@[simp, norm_cast]
theorem one_le_cast : 1 â‰¤ (n : Î±) â†” 1 â‰¤ n := by rw [â† cast_one, cast_le]
                                                -- ðŸŽ‰ no goals
#align nat.one_le_cast Nat.one_le_cast

@[simp, norm_cast]
theorem cast_lt_one : (n : Î±) < 1 â†” n = 0 := by
  rw [â† cast_one, cast_lt, lt_succ_iff, â† bot_eq_zero, le_bot_iff]
  -- ðŸŽ‰ no goals
#align nat.cast_lt_one Nat.cast_lt_one

@[simp, norm_cast]
theorem cast_le_one : (n : Î±) â‰¤ 1 â†” n â‰¤ 1 := by rw [â† cast_one, cast_le]
                                                -- ðŸŽ‰ no goals
#align nat.cast_le_one Nat.cast_le_one

end OrderedSemiring

/-- A version of `Nat.cast_sub` that works for `â„â‰¥0` and `â„šâ‰¥0`. Note that this proof doesn't work
for `â„•âˆž` and `â„â‰¥0âˆž`, so we use type-specific lemmas for these types. -/
@[simp, norm_cast]
theorem cast_tsub [CanonicallyOrderedCommSemiring Î±] [Sub Î±] [OrderedSub Î±]
    [ContravariantClass Î± Î± (Â· + Â·) (Â· â‰¤ Â·)] (m n : â„•) : â†‘(m - n) = (m - n : Î±) := by
  cases' le_total m n with h h
  -- âŠ¢ â†‘(m - n) = â†‘m - â†‘n
  Â· rw [tsub_eq_zero_of_le h, cast_zero, tsub_eq_zero_of_le]
    -- âŠ¢ â†‘m â‰¤ â†‘n
    exact mono_cast h
    -- ðŸŽ‰ no goals
  Â· rcases le_iff_exists_add'.mp h with âŸ¨m, rflâŸ©
    -- âŠ¢ â†‘(m + n - n) = â†‘(m + n) - â†‘n
    rw [add_tsub_cancel_right, cast_add, add_tsub_cancel_right]
    -- ðŸŽ‰ no goals
#align nat.cast_tsub Nat.cast_tsub

@[simp, norm_cast]
theorem cast_min [LinearOrderedSemiring Î±] {a b : â„•} : ((min a b : â„•) : Î±) = min (a : Î±) b :=
  (@mono_cast Î± _).map_min
#align nat.cast_min Nat.cast_min

@[simp, norm_cast]
theorem cast_max [LinearOrderedSemiring Î±] {a b : â„•} : ((max a b : â„•) : Î±) = max (a : Î±) b :=
  (@mono_cast Î± _).map_max
#align nat.cast_max Nat.cast_max

@[simp, norm_cast]
theorem abs_cast [LinearOrderedRing Î±] (a : â„•) : |(a : Î±)| = a :=
  abs_of_nonneg (cast_nonneg a)
#align nat.abs_cast Nat.abs_cast

theorem coe_nat_dvd [Semiring Î±] {m n : â„•} (h : m âˆ£ n) : (m : Î±) âˆ£ (n : Î±) :=
  map_dvd (Nat.castRingHom Î±) h
#align nat.coe_nat_dvd Nat.coe_nat_dvd

alias _root_.Dvd.dvd.natCast := coe_nat_dvd

end Nat

instance [AddMonoidWithOne Î±] [CharZero Î±] : Nontrivial Î± where exists_pair_ne :=
  âŸ¨1, 0, (Nat.cast_one (R := Î±) â–¸ Nat.cast_ne_zero.2 (by decide))âŸ©
                                                         -- ðŸŽ‰ no goals

section AddMonoidHomClass

variable {A B F : Type*} [AddMonoidWithOne B]

theorem ext_nat' [AddMonoid A] [AddMonoidHomClass F â„• A] (f g : F) (h : f 1 = g 1) : f = g :=
  FunLike.ext f g <| by
    intro n
    -- âŠ¢ â†‘f n = â†‘g n
    induction n with
    | zero => simp_rw [Nat.zero_eq, map_zero f, map_zero g]
    | succ n ihn =>
      simp [Nat.succ_eq_add_one, h, ihn]
#align ext_nat' ext_nat'

@[ext]
theorem AddMonoidHom.ext_nat [AddMonoid A] {f g : â„• â†’+ A} : f 1 = g 1 â†’ f = g :=
  ext_nat' f g
#align add_monoid_hom.ext_nat AddMonoidHom.ext_nat

variable [AddMonoidWithOne A]

-- these versions are primed so that the `RingHomClass` versions aren't
theorem eq_natCast' [AddMonoidHomClass F â„• A] (f : F) (h1 : f 1 = 1) : âˆ€ n : â„•, f n = n
  | 0 => by simp [map_zero f]
            -- ðŸŽ‰ no goals
  | n + 1 => by rw [map_add, h1, eq_natCast' f h1 n, Nat.cast_add_one]
                -- ðŸŽ‰ no goals
#align eq_nat_cast' eq_natCast'

theorem map_natCast' {A} [AddMonoidWithOne A] [AddMonoidHomClass F A B] (f : F) (h : f 1 = 1) :
    âˆ€ n : â„•, f n = n
  | 0 => by simp [map_zero f]
            -- ðŸŽ‰ no goals
  | n + 1 => by
    rw [Nat.cast_add, map_add, Nat.cast_add, map_natCast' f h n, Nat.cast_one, h, Nat.cast_one]
    -- ðŸŽ‰ no goals
#align map_nat_cast' map_natCast'

end AddMonoidHomClass

section MonoidWithZeroHomClass

variable {A F : Type*} [MulZeroOneClass A]

/-- If two `MonoidWithZeroHom`s agree on the positive naturals they are equal. -/
theorem ext_nat'' [MonoidWithZeroHomClass F â„• A] (f g : F) (h_pos : âˆ€ {n : â„•}, 0 < n â†’ f n = g n) :
    f = g := by
  apply FunLike.ext
  -- âŠ¢ âˆ€ (x : â„•), â†‘f x = â†‘g x
  rintro (_ | n)
  -- âŠ¢ â†‘f Nat.zero = â†‘g Nat.zero
  Â· simp [map_zero f, map_zero g]
    -- ðŸŽ‰ no goals
  Â· exact h_pos n.succ_pos
    -- ðŸŽ‰ no goals
#align ext_nat'' ext_nat''

@[ext]
theorem MonoidWithZeroHom.ext_nat {f g : â„• â†’*â‚€ A} : (âˆ€ {n : â„•}, 0 < n â†’ f n = g n) â†’ f = g :=
  ext_nat'' f g
#align monoid_with_zero_hom.ext_nat MonoidWithZeroHom.ext_nat

end MonoidWithZeroHomClass

section RingHomClass

variable {R S F : Type*} [NonAssocSemiring R] [NonAssocSemiring S]

@[simp]
theorem eq_natCast [RingHomClass F â„• R] (f : F) : âˆ€ n, f n = n :=
  eq_natCast' f <| map_one f
#align eq_nat_cast eq_natCast

@[simp]
theorem map_natCast [RingHomClass F R S] (f : F) : âˆ€ n : â„•, f (n : R) = n :=
  map_natCast' f <| map_one f
#align map_nat_cast map_natCast

--Porting note: new theorem
@[simp]
theorem map_ofNat [RingHomClass F R S] (f : F) (n : â„•) [Nat.AtLeastTwo n] :
    (f (no_index (OfNat.ofNat n)) : S) = OfNat.ofNat n :=
  map_natCast f n

theorem ext_nat [RingHomClass F â„• R] (f g : F) : f = g :=
  ext_nat' f g <| by simp only [map_one f, map_one g]
                     -- ðŸŽ‰ no goals
#align ext_nat ext_nat

theorem NeZero.nat_of_injective {n : â„•} [h : NeZero (n : R)] [RingHomClass F R S] {f : F}
    (hf : Function.Injective f) : NeZero (n : S) :=
  âŸ¨fun h â†¦ NeZero.natCast_ne n R <| hf <| by simpa only [map_natCast, map_zero f] âŸ©
                                             -- ðŸŽ‰ no goals
#align ne_zero.nat_of_injective NeZero.nat_of_injective

theorem NeZero.nat_of_neZero {R S} [Semiring R] [Semiring S] {F} [RingHomClass F R S] (f : F)
    {n : â„•} [hn : NeZero (n : S)] : NeZero (n : R) :=
  .of_map (f := f) (neZero := by simp only [map_natCast, hn])
                                 -- ðŸŽ‰ no goals
#align ne_zero.nat_of_ne_zero NeZero.nat_of_neZero

end RingHomClass

namespace RingHom

/-- This is primed to match `eq_intCast'`. -/
theorem eq_natCast' {R} [NonAssocSemiring R] (f : â„• â†’+* R) : f = Nat.castRingHom R :=
  RingHom.ext <| eq_natCast f
#align ring_hom.eq_nat_cast' RingHom.eq_natCast'

end RingHom

@[simp, norm_cast]
theorem Nat.cast_id (n : â„•) : n.cast = n :=
  rfl
#align nat.cast_id Nat.cast_id

@[simp]
theorem Nat.castRingHom_nat : Nat.castRingHom â„• = RingHom.id â„• :=
  rfl
#align nat.cast_ring_hom_nat Nat.castRingHom_nat

/-- We don't use `RingHomClass` here, since that might cause type-class slowdown for
`Subsingleton`-/
instance Nat.uniqueRingHom {R : Type*} [NonAssocSemiring R] : Unique (â„• â†’+* R) where
  default := Nat.castRingHom R
  uniq := RingHom.eq_natCast'

namespace Pi

variable {Ï€ : Î± â†’ Type*} [âˆ€ a, NatCast (Ï€ a)]

/- Porting note: manually wrote this instance.
Was `by refine_struct { .. } <;> pi_instance_derive_field` -/
instance natCast : NatCast (âˆ€ a, Ï€ a) := { natCast := fun n _ â†¦ n }

theorem nat_apply (n : â„•) (a : Î±) : (n : âˆ€ a, Ï€ a) a = n :=
  rfl
#align pi.nat_apply Pi.nat_apply

@[simp]
theorem coe_nat (n : â„•) : (n : âˆ€ a, Ï€ a) = fun _ â†¦ â†‘n :=
  rfl
#align pi.coe_nat Pi.coe_nat

@[simp]
theorem ofNat_apply (n : â„•) [n.AtLeastTwo] (a : Î±) : (OfNat.ofNat n : âˆ€ a, Ï€ a) a = n := rfl

end Pi

theorem Sum.elim_natCast_natCast {Î± Î² Î³ : Type*} [NatCast Î³] (n : â„•) :
    Sum.elim (n : Î± â†’ Î³) (n : Î² â†’ Î³) = n :=
  @Sum.elim_lam_const_lam_const Î± Î² Î³ n
#align sum.elim_nat_cast_nat_cast Sum.elim_natCast_natCast

/-! ### Order dual -/


open OrderDual

instance [h : NatCast Î±] : NatCast Î±áµ’áµˆ :=
  h

instance [h : AddMonoidWithOne Î±] : AddMonoidWithOne Î±áµ’áµˆ :=
  h

instance [h : AddCommMonoidWithOne Î±] : AddCommMonoidWithOne Î±áµ’áµˆ :=
  h

@[simp]
theorem toDual_natCast [NatCast Î±] (n : â„•) : toDual (n : Î±) = n :=
  rfl
#align to_dual_nat_cast toDual_natCast

@[simp]
theorem ofDual_natCast [NatCast Î±] (n : â„•) : (ofDual n : Î±) = n :=
  rfl
#align of_dual_nat_cast ofDual_natCast

/-! ### Lexicographic order -/


instance [h : NatCast Î±] : NatCast (Lex Î±) :=
  h

instance [h : AddMonoidWithOne Î±] : AddMonoidWithOne (Lex Î±) :=
  h

instance [h : AddCommMonoidWithOne Î±] : AddCommMonoidWithOne (Lex Î±) :=
  h

@[simp]
theorem toLex_natCast [NatCast Î±] (n : â„•) : toLex (n : Î±) = n :=
  rfl
#align to_lex_nat_cast toLex_natCast

@[simp]
theorem ofLex_natCast [NatCast Î±] (n : â„•) : (ofLex n : Î±) = n :=
  rfl
#align of_lex_nat_cast ofLex_natCast
