/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Patrick Stevens
-/
import Mathlib.Data.Nat.Choose.Basic
import Mathlib.Tactic.Linarith
import Mathlib.Algebra.BigOperators.Ring
import Mathlib.Algebra.BigOperators.Intervals
import Mathlib.Algebra.BigOperators.Order
import Mathlib.Algebra.BigOperators.NatAntidiagonal

#align_import data.nat.choose.sum from "leanprover-community/mathlib"@"4c19a16e4b705bf135cf9a80ac18fcc99c438514"

/-!
# Sums of binomial coefficients

This file includes variants of the binomial theorem and other results on sums of binomial
coefficients. Theorems whose proofs depend on such sums may also go in this file for import
reasons.

-/


open Nat

open Finset

open BigOperators

variable {R : Type*}

namespace Commute

variable [Semiring R] {x y : R}

/-- A version of the **binomial theorem** for commuting elements in noncommutative semirings. -/
theorem add_pow (h : Commute x y) (n : â„•) :
    (x + y) ^ n = âˆ‘ m in range (n + 1), x ^ m * y ^ (n - m) * choose n m := by
  let t : â„• â†’ â„• â†’ R := fun n m â†¦ x ^ m * y ^ (n - m) * choose n m
  -- âŠ¢ (x + y) ^ n = âˆ‘ m in range (n + 1), x ^ m * y ^ (n - m) * â†‘(Nat.choose n m)
  change (x + y) ^ n = âˆ‘ m in range (n + 1), t n m
  -- âŠ¢ (x + y) ^ n = âˆ‘ m in range (n + 1), t n m
  have h_first : âˆ€ n, t n 0 = y ^ n := fun n â†¦ by
    simp only [choose_zero_right, _root_.pow_zero, Nat.cast_one, mul_one, one_mul, tsub_zero]
  have h_last : âˆ€ n, t n n.succ = 0 := fun n â†¦ by
    simp only [ge_iff_le, choose_succ_self, cast_zero, mul_zero]
  have h_middle :
    âˆ€ n i : â„•, i âˆˆ range n.succ â†’ (t n.succ âˆ˜ Nat.succ) i =
      x * t n i + y * t n i.succ := by
    intro n i h_mem
    have h_le : i â‰¤ n := Nat.le_of_lt_succ (mem_range.mp h_mem)
    dsimp only
    rw [Function.comp_apply, choose_succ_succ, Nat.cast_add, mul_add]
    congr 1
    Â· rw [pow_succ x, succ_sub_succ, mul_assoc, mul_assoc, mul_assoc]
    Â· rw [â† mul_assoc y, â† mul_assoc y, (h.symm.pow_right i.succ).eq]
      by_cases h_eq : i = n
      Â· rw [h_eq, choose_succ_self, Nat.cast_zero, mul_zero, mul_zero]
      Â· rw [succ_sub (lt_of_le_of_ne h_le h_eq)]
        rw [pow_succ y, mul_assoc, mul_assoc, mul_assoc, mul_assoc]
  induction' n with n ih
  -- âŠ¢ (x + y) ^ zero = âˆ‘ m in range (zero + 1), t zero m
  Â· rw [_root_.pow_zero, sum_range_succ, range_zero, sum_empty, zero_add]
    -- âŠ¢ 1 = t zero zero
    dsimp only
    -- âŠ¢ 1 = x ^ zero * y ^ (zero - zero) * â†‘(Nat.choose zero zero)
    rw [_root_.pow_zero, _root_.pow_zero, choose_self, Nat.cast_one, mul_one, mul_one]
    -- ğŸ‰ no goals
  Â· rw [sum_range_succ', h_first]
    -- âŠ¢ (x + y) ^ succ n = âˆ‘ k in range (n + 1), t (succ n) (k + 1) + y ^ succ n
    erw [sum_congr rfl (h_middle n), sum_add_distrib, add_assoc]
    -- âŠ¢ (x + y) ^ succ n = âˆ‘ x_1 in range (succ n), x * t n x_1 + (âˆ‘ x in range (suc â€¦
    rw [pow_succ (x + y), ih, add_mul, mul_sum, mul_sum]
    -- âŠ¢ âˆ‘ x_1 in range (n + 1), x * t n x_1 + âˆ‘ x in range (n + 1), y * t n x = âˆ‘ x_ â€¦
    congr 1
    -- âŠ¢ âˆ‘ x in range (n + 1), y * t n x = âˆ‘ x in range (succ n), y * t n (succ x) +  â€¦
    rw [sum_range_succ', sum_range_succ, h_first, h_last, mul_zero, add_zero, _root_.pow_succ]
    -- ğŸ‰ no goals
#align commute.add_pow Commute.add_pow

/-- A version of `Commute.add_pow` that avoids â„•-subtraction by summing over the antidiagonal and
also with the binomial coefficient applied via scalar action of â„•. -/
theorem add_pow' (h : Commute x y) (n : â„•) :
    (x + y) ^ n = âˆ‘ m in Nat.antidiagonal n, choose n m.fst â€¢ (x ^ m.fst * y ^ m.snd) := by
  simp_rw [Finset.Nat.sum_antidiagonal_eq_sum_range_succ fun m p â†¦ choose n m â€¢ (x ^ m * y ^ p),
    _root_.nsmul_eq_mul, cast_comm, h.add_pow]
#align commute.add_pow' Commute.add_pow'

end Commute

/-- The **binomial theorem** -/
theorem add_pow [CommSemiring R] (x y : R) (n : â„•) :
    (x + y) ^ n = âˆ‘ m in range (n + 1), x ^ m * y ^ (n - m) * choose n m :=
  (Commute.all x y).add_pow n
#align add_pow add_pow

namespace Nat

/-- The sum of entries in a row of Pascal's triangle -/
theorem sum_range_choose (n : â„•) : (âˆ‘ m in range (n + 1), choose n m) = 2 ^ n := by
  have := (add_pow 1 1 n).symm
  -- âŠ¢ âˆ‘ m in range (n + 1), choose n m = 2 ^ n
  simpa [one_add_one_eq_two] using this
  -- ğŸ‰ no goals
#align nat.sum_range_choose Nat.sum_range_choose

theorem sum_range_choose_halfway (m : Nat) : (âˆ‘ i in range (m + 1), choose (2 * m + 1) i) = 4 ^ m :=
  have : (âˆ‘ i in range (m + 1), choose (2 * m + 1) (2 * m + 1 - i)) =
      âˆ‘ i in range (m + 1), choose (2 * m + 1) i :=
    sum_congr rfl fun i hi â†¦ choose_symm <| by linarith [mem_range.1 hi]
                                               -- ğŸ‰ no goals
  mul_right_injectiveâ‚€ two_ne_zero <|
    calc
      (2 * âˆ‘ i in range (m + 1), choose (2 * m + 1) i) =
          (âˆ‘ i in range (m + 1), choose (2 * m + 1) i) +
            âˆ‘ i in range (m + 1), choose (2 * m + 1) (2 * m + 1 - i) := by rw [two_mul, this]
                                                                           -- ğŸ‰ no goals
      _ = (âˆ‘ i in range (m + 1), choose (2 * m + 1) i) +
            âˆ‘ i in Ico (m + 1) (2 * m + 2), choose (2 * m + 1) i := by
        { rw [range_eq_Ico, sum_Ico_reflect]
          Â· congr
            have A : m + 1 â‰¤ 2 * m + 1 := by linarith
            rw [add_comm, add_tsub_assoc_of_le A, â† add_comm]
            congr
            rw [tsub_eq_iff_eq_add_of_le A]
            ring
          Â· linarith }
      _ = âˆ‘ i in range (2 * m + 2), choose (2 * m + 1) i := sum_range_add_sum_Ico _ (by linarith)
                                                                                        -- ğŸ‰ no goals
      _ = 2 ^ (2 * m + 1) := sum_range_choose (2 * m + 1)
      _ = 2 * 4 ^ m := by rw [pow_succ, pow_mul, mul_comm]; rfl
                          -- âŠ¢ 2 * (2 ^ 2) ^ m = 2 * 4 ^ m
                                                            -- ğŸ‰ no goals
#align nat.sum_range_choose_halfway Nat.sum_range_choose_halfway

theorem choose_middle_le_pow (n : â„•) : choose (2 * n + 1) n â‰¤ 4 ^ n := by
  have t : choose (2 * n + 1) n â‰¤ âˆ‘ i in range (n + 1), choose (2 * n + 1) i :=
    single_le_sum (fun x _ â†¦ by linarith) (self_mem_range_succ n)
  simpa [sum_range_choose_halfway n] using t
  -- ğŸ‰ no goals
#align nat.choose_middle_le_pow Nat.choose_middle_le_pow

theorem four_pow_le_two_mul_add_one_mul_central_binom (n : â„•) :
    4 ^ n â‰¤ (2 * n + 1) * choose (2 * n) n :=
  calc
    4 ^ n = (1 + 1) ^ (2 * n) := by norm_num [pow_mul]
                                    -- ğŸ‰ no goals
    _ = âˆ‘ m in range (2 * n + 1), choose (2 * n) m := by simp [add_pow]
                                                         -- ğŸ‰ no goals
    _ â‰¤ âˆ‘ m in range (2 * n + 1), choose (2 * n) (2 * n / 2) := by gcongr; apply choose_le_middle
                                                                   -- âŠ¢ choose (2 * n) iâœ â‰¤ choose (2 * n) (2 * n / 2)
                                                                           -- ğŸ‰ no goals
    _ = (2 * n + 1) * choose (2 * n) n := by simp
                                             -- ğŸ‰ no goals
#align nat.four_pow_le_two_mul_add_one_mul_central_binom Nat.four_pow_le_two_mul_add_one_mul_central_binom

end Nat

theorem Int.alternating_sum_range_choose {n : â„•} :
    (âˆ‘ m in range (n + 1), ((-1) ^ m * â†‘(choose n m) : â„¤)) = if n = 0 then 1 else 0 := by
  cases n; Â· simp
  -- âŠ¢ âˆ‘ m in range (zero + 1), (-1) ^ m * â†‘(Nat.choose zero m) = if zero = 0 then  â€¦
             -- ğŸ‰ no goals
  case succ n =>
    have h := add_pow (-1 : â„¤) 1 n.succ
    simp only [one_pow, mul_one, add_left_neg] at h
    rw [â† h, zero_pow (Nat.succ_pos n), if_neg (Nat.succ_ne_zero n)]
#align int.alternating_sum_range_choose Int.alternating_sum_range_choose

theorem Int.alternating_sum_range_choose_of_ne {n : â„•} (h0 : n â‰  0) :
    (âˆ‘ m in range (n + 1), ((-1) ^ m * â†‘(choose n m) : â„¤)) = 0 := by
  rw [Int.alternating_sum_range_choose, if_neg h0]
  -- ğŸ‰ no goals
#align int.alternating_sum_range_choose_of_ne Int.alternating_sum_range_choose_of_ne

namespace Finset

theorem sum_powerset_apply_card {Î± Î² : Type*} [AddCommMonoid Î±] (f : â„• â†’ Î±) {x : Finset Î²} :
    âˆ‘ m in x.powerset, f m.card = âˆ‘ m in range (x.card + 1), x.card.choose m â€¢ f m := by
  trans âˆ‘ m in range (x.card + 1), âˆ‘ j in x.powerset.filter fun z â†¦ z.card = m, f j.card
  -- âŠ¢ âˆ‘ m in powerset x, f (card m) = âˆ‘ m in range (card x + 1), âˆ‘ j in filter (fu â€¦
  Â· refine' (sum_fiberwise_of_maps_to _ _).symm
    -- âŠ¢ âˆ€ (x_1 : Finset Î²), x_1 âˆˆ powerset x â†’ card x_1 âˆˆ range (card x + 1)
    intro y hy
    -- âŠ¢ card y âˆˆ range (card x + 1)
    rw [mem_range, Nat.lt_succ_iff]
    -- âŠ¢ card y â‰¤ card x
    rw [mem_powerset] at hy
    -- âŠ¢ card y â‰¤ card x
    exact card_le_of_subset hy
    -- ğŸ‰ no goals
  Â· refine' sum_congr rfl fun y _ â†¦ _
    -- âŠ¢ âˆ‘ j in filter (fun z => card z = y) (powerset x), f (card j) = Nat.choose (c â€¦
    rw [â† card_powersetLen, â† sum_const]
    -- âŠ¢ âˆ‘ j in filter (fun z => card z = y) (powerset x), f (card j) = âˆ‘ _x in power â€¦
    refine' sum_congr powersetLen_eq_filter.symm fun z hz â†¦ _
    -- âŠ¢ f (card z) = f y
    rw [(mem_powersetLen.1 hz).2]
    -- ğŸ‰ no goals
#align finset.sum_powerset_apply_card Finset.sum_powerset_apply_card

theorem sum_powerset_neg_one_pow_card {Î± : Type*} [DecidableEq Î±] {x : Finset Î±} :
    (âˆ‘ m in x.powerset, (-1 : â„¤) ^ m.card) = if x = âˆ… then 1 else 0 := by
  rw [sum_powerset_apply_card]
  -- âŠ¢ âˆ‘ m in range (card x + 1), Nat.choose (card x) m â€¢ (-1) ^ m = if x = âˆ… then  â€¦
  simp only [nsmul_eq_mul', â† card_eq_zero, Int.alternating_sum_range_choose]
  -- ğŸ‰ no goals
#align finset.sum_powerset_neg_one_pow_card Finset.sum_powerset_neg_one_pow_card

theorem sum_powerset_neg_one_pow_card_of_nonempty {Î± : Type*} {x : Finset Î±} (h0 : x.Nonempty) :
    (âˆ‘ m in x.powerset, (-1 : â„¤) ^ m.card) = 0 := by
  classical
    rw [sum_powerset_neg_one_pow_card, if_neg]
    rw [â† Ne.def, â† nonempty_iff_ne_empty]
    apply h0
#align finset.sum_powerset_neg_one_pow_card_of_nonempty Finset.sum_powerset_neg_one_pow_card_of_nonempty

variable {M R : Type*} [CommMonoid M] [NonAssocSemiring R]

-- porting note: new lemma
@[to_additive sum_choose_succ_nsmul]
theorem prod_pow_choose_succ {M : Type*} [CommMonoid M] (f : â„• â†’ â„• â†’ M) (n : â„•) :
    (âˆ i in range (n + 2), f i (n + 1 - i) ^ (n + 1).choose i) =
      (âˆ i in range (n + 1), f i (n + 1 - i) ^ n.choose i) *
        âˆ i in range (n + 1), f (i + 1) (n - i) ^ n.choose i := by
  have A : (âˆ i in range (n + 1), f (i + 1) (n - i) ^ (n.choose (i + 1))) * f 0 (n + 1) =
    âˆ i in range (n + 1), f i (n + 1 - i) ^ (n.choose i)
  Â· rw [prod_range_succ, prod_range_succ']
    -- âŠ¢ (âˆ x in range n, f (x + 1) (n - x) ^ Nat.choose n (x + 1)) * f (n + 1) (n -  â€¦
    simp
    -- ğŸ‰ no goals
  rw [prod_range_succ']
  -- âŠ¢ (âˆ k in range (n + 1), f (k + 1) (n + 1 - (k + 1)) ^ Nat.choose (n + 1) (k + â€¦
  simpa [Nat.choose_succ_succ, pow_add, prod_mul_distrib, A, mul_assoc] using mul_comm _ _
  -- ğŸ‰ no goals

-- porting note: new lemma
@[to_additive sum_antidiagonal_choose_succ_nsmul]
theorem prod_antidiagonal_pow_choose_succ {M : Type*} [CommMonoid M] (f : â„• â†’ â„• â†’ M) (n : â„•) :
    (âˆ ij in Nat.antidiagonal (n + 1), f ij.1 ij.2 ^ (n + 1).choose ij.1) =
      (âˆ ij in Nat.antidiagonal n, f ij.1 (ij.2 + 1) ^ n.choose ij.1) *
        âˆ ij in Nat.antidiagonal n, f (ij.1 + 1) ij.2 ^ n.choose ij.2 := by
  simp only [Nat.prod_antidiagonal_eq_prod_range_succ_mk, prod_pow_choose_succ]
  -- âŠ¢ (âˆ i in range (n + 1), f i (n + 1 - i) ^ Nat.choose n i) * âˆ i in range (n + â€¦
  have : âˆ€ i âˆˆ range (n + 1), i â‰¤ n := fun i hi â†¦ by simpa [Nat.lt_succ_iff] using hi
  -- âŠ¢ (âˆ i in range (n + 1), f i (n + 1 - i) ^ Nat.choose n i) * âˆ i in range (n + â€¦
  congr 1
  -- âŠ¢ âˆ i in range (n + 1), f i (n + 1 - i) ^ Nat.choose n i = âˆ x in range (succ  â€¦
  Â· refine prod_congr rfl fun i hi â†¦ ?_
    -- âŠ¢ f i (n + 1 - i) ^ Nat.choose n i = f i (n - i + 1) ^ Nat.choose n i
    rw [tsub_add_eq_add_tsub (this _ hi)]
    -- ğŸ‰ no goals
  Â· refine prod_congr rfl fun i hi â†¦ ?_
    -- âŠ¢ f (i + 1) (n - i) ^ Nat.choose n i = f (i + 1) (n - i) ^ Nat.choose n (n - i)
    rw [Nat.choose_symm (this _ hi)]
    -- ğŸ‰ no goals

-- porting note: moved from `Mathlib.Analysis.Calculus.ContDiff`
/-- The sum of `(n+1).choose i * f i (n+1-i)` can be split into two sums at rank `n`,
respectively of `n.choose i * f i (n+1-i)` and `n.choose i * f (i+1) (n-i)`. -/
theorem sum_choose_succ_mul (f : â„• â†’ â„• â†’ R) (n : â„•) :
    (âˆ‘ i in range (n + 2), ((n + 1).choose i : R) * f i (n + 1 - i)) =
      (âˆ‘ i in range (n + 1), (n.choose i : R) * f i (n + 1 - i)) +
        âˆ‘ i in range (n + 1), (n.choose i : R) * f (i + 1) (n - i) := by
  simpa only [nsmul_eq_mul] using sum_choose_succ_nsmul f n
  -- ğŸ‰ no goals
#align finset.sum_choose_succ_mul Finset.sum_choose_succ_mul

/-- The sum along the antidiagonal of `(n+1).choose i * f i j` can be split into two sums along the
antidiagonal at rank `n`, respectively of `n.choose i * f i (j+1)` and `n.choose j * f (i+1) j`. -/
theorem sum_antidiagonal_choose_succ_mul (f : â„• â†’ â„• â†’ R) (n : â„•) :
    (âˆ‘ ij in Nat.antidiagonal (n + 1), ((n + 1).choose ij.1 : R) * f ij.1 ij.2) =
      (âˆ‘ ij in Nat.antidiagonal n, (n.choose ij.1 : R) * f ij.1 (ij.2 + 1)) +
        âˆ‘ ij in Nat.antidiagonal n, (n.choose ij.2 : R) * f (ij.1 + 1) ij.2 := by
  simpa only [nsmul_eq_mul] using sum_antidiagonal_choose_succ_nsmul f n
  -- ğŸ‰ no goals
#align finset.sum_antidiagonal_choose_succ_mul Finset.sum_antidiagonal_choose_succ_mul

end Finset
