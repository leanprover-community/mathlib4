/-
Copyright (c) 2018 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
import Mathlib.Data.Finsupp.Basic
import Mathlib.Data.Finsupp.Order

#align_import data.finsupp.multiset from "leanprover-community/mathlib"@"59694bd07f0a39c5beccba34bd9f413a160782bf"

/-!
# Equivalence between `Multiset` and `â„•`-valued finitely supported functions

This defines `Finsupp.toMultiset` the equivalence between `Î± â†’â‚€ â„•` and `Multiset Î±`, along
with `Multiset.toFinsupp` the reverse equivalence and `Finsupp.orderIsoMultiset` the equivalence
promoted to an order isomorphism.
-/


open Finset

open BigOperators

variable {Î± Î² Î¹ : Type*}

namespace Finsupp

/-- Given `f : Î± â†’â‚€ â„•`, `f.toMultiset` is the multiset with multiplicities given by the values of
`f` on the elements of `Î±`. We define this function as an `AddMonoidHom`.

Under the additional assumption of `[DecidableEq Î±]`, this is available as
`Multiset.toFinsupp : Multiset Î± â‰ƒ+ (Î± â†’â‚€ â„•)`; the two declarations are separate as this assumption
is only needed for one direction. -/
def toMultiset : (Î± â†’â‚€ â„•) â†’+ Multiset Î± where
  toFun f := Finsupp.sum f fun a n => n â€¢ {a}
  -- Porting note: times out if h is not specified
  map_add' _f _g := sum_add_index' (h := fun a n => n â€¢ ({a} : Multiset Î±))
    (fun _ â†¦ zero_nsmul _) (fun _ â†¦ add_nsmul _)
  map_zero' := sum_zero_index

theorem toMultiset_zero : toMultiset (0 : Î± â†’â‚€ â„•) = 0 :=
  rfl
#align finsupp.to_multiset_zero Finsupp.toMultiset_zero

theorem toMultiset_add (m n : Î± â†’â‚€ â„•) : toMultiset (m + n) = toMultiset m + toMultiset n :=
  toMultiset.map_add m n
#align finsupp.to_multiset_add Finsupp.toMultiset_add

theorem toMultiset_apply (f : Î± â†’â‚€ â„•) : toMultiset f = f.sum fun a n => n â€¢ {a} :=
  rfl
#align finsupp.to_multiset_apply Finsupp.toMultiset_apply

@[simp]
theorem toMultiset_single (a : Î±) (n : â„•) : toMultiset (single a n) = n â€¢ {a} := by
  rw [toMultiset_apply, sum_single_index]; apply zero_nsmul
  -- âŠ¢ 0 â€¢ {a} = 0
                                           -- ğŸ‰ no goals
#align finsupp.to_multiset_single Finsupp.toMultiset_single

theorem toMultiset_sum {f : Î¹ â†’ Î± â†’â‚€ â„•} (s : Finset Î¹) :
    Finsupp.toMultiset (âˆ‘ i in s, f i) = âˆ‘ i in s, Finsupp.toMultiset (f i) :=
  map_sum Finsupp.toMultiset _ _
#align finsupp.to_multiset_sum Finsupp.toMultiset_sum

theorem toMultiset_sum_single (s : Finset Î¹) (n : â„•) :
    Finsupp.toMultiset (âˆ‘ i in s, single i n) = n â€¢ s.val := by
  simp_rw [toMultiset_sum, Finsupp.toMultiset_single, sum_nsmul, sum_multiset_singleton]
  -- ğŸ‰ no goals
#align finsupp.to_multiset_sum_single Finsupp.toMultiset_sum_single

theorem card_toMultiset (f : Î± â†’â‚€ â„•) : Multiset.card (toMultiset f) = f.sum fun _ => id := by
  simp [toMultiset_apply, map_finsupp_sum, Function.id_def]
  -- ğŸ‰ no goals
#align finsupp.card_to_multiset Finsupp.card_toMultiset

theorem toMultiset_map (f : Î± â†’â‚€ â„•) (g : Î± â†’ Î²) :
    f.toMultiset.map g = toMultiset (f.mapDomain g) := by
  refine' f.induction _ _
  -- âŠ¢ Multiset.map g (â†‘toMultiset 0) = â†‘toMultiset (mapDomain g 0)
  Â· rw [toMultiset_zero, Multiset.map_zero, mapDomain_zero, toMultiset_zero]
    -- ğŸ‰ no goals
  Â· intro a n f _ _ ih
    -- âŠ¢ Multiset.map g (â†‘toMultiset (single a n + f)) = â†‘toMultiset (mapDomain g (si â€¦
    rw [toMultiset_add, Multiset.map_add, ih, mapDomain_add, mapDomain_single,
      toMultiset_single, toMultiset_add, toMultiset_single, â† Multiset.coe_mapAddMonoidHom,
      (Multiset.mapAddMonoidHom g).map_nsmul]
    rfl
    -- ğŸ‰ no goals
#align finsupp.to_multiset_map Finsupp.toMultiset_map

@[simp]
theorem prod_toMultiset [CommMonoid Î±] (f : Î± â†’â‚€ â„•) :
    f.toMultiset.prod = f.prod fun a n => a ^ n := by
  refine' f.induction _ _
  -- âŠ¢ Multiset.prod (â†‘toMultiset 0) = prod 0 fun a n => a ^ n
  Â· rw [toMultiset_zero, Multiset.prod_zero, Finsupp.prod_zero_index]
    -- ğŸ‰ no goals
  Â· intro a n f _ _ ih
    -- âŠ¢ Multiset.prod (â†‘toMultiset (single a n + f)) = prod (single a n + f) fun a n â€¦
    rw [toMultiset_add, Multiset.prod_add, ih, toMultiset_single, Multiset.prod_nsmul,
      Finsupp.prod_add_index' pow_zero pow_add, Finsupp.prod_single_index, Multiset.prod_singleton]
    Â· exact pow_zero a
      -- ğŸ‰ no goals
#align finsupp.prod_to_multiset Finsupp.prod_toMultiset

@[simp]
theorem toFinset_toMultiset [DecidableEq Î±] (f : Î± â†’â‚€ â„•) : f.toMultiset.toFinset = f.support := by
  refine' f.induction _ _
  -- âŠ¢ Multiset.toFinset (â†‘toMultiset 0) = 0.support
  Â· rw [toMultiset_zero, Multiset.toFinset_zero, support_zero]
    -- ğŸ‰ no goals
  Â· intro a n f ha hn ih
    -- âŠ¢ Multiset.toFinset (â†‘toMultiset (single a n + f)) = (single a n + f).support
    rw [toMultiset_add, Multiset.toFinset_add, ih, toMultiset_single, support_add_eq,
      support_single_ne_zero _ hn, Multiset.toFinset_nsmul _ _ hn, Multiset.toFinset_singleton]
    refine' Disjoint.mono_left support_single_subset _
    -- âŠ¢ Disjoint {a} f.support
    rwa [Finset.disjoint_singleton_left]
    -- ğŸ‰ no goals
#align finsupp.to_finset_to_multiset Finsupp.toFinset_toMultiset

@[simp]
theorem count_toMultiset [DecidableEq Î±] (f : Î± â†’â‚€ â„•) (a : Î±) : (toMultiset f).count a = f a :=
  calc
    (toMultiset f).count a = Finsupp.sum f (fun x n => (n â€¢ {x} : Multiset Î±).count a) :=
      by rw [toMultiset_apply]; exact map_sum (Multiset.countAddMonoidHom a) _ f.support
         -- âŠ¢ Multiset.count a (sum f fun a n => n â€¢ {a}) = sum f fun x n => Multiset.coun â€¦
                                -- ğŸ‰ no goals
    _ = f.sum fun x n => n * ({x} : Multiset Î±).count a := by simp only [Multiset.count_nsmul]
                                                              -- ğŸ‰ no goals
    _ = f a * ({a} : Multiset Î±).count a :=
      sum_eq_single _
        (fun a' _ H => by simp only [Multiset.count_singleton, if_false, H.symm, mul_zero]) fun H =>
                          -- ğŸ‰ no goals
        by simp only [not_mem_support_iff.1 H, zero_mul]
           -- ğŸ‰ no goals
    _ = f a := by rw [Multiset.count_singleton_self, mul_one]
                  -- ğŸ‰ no goals
#align finsupp.count_to_multiset Finsupp.count_toMultiset

theorem toMultiset_sup [DecidableEq Î±] (f g : Î± â†’â‚€ â„•) :
    toMultiset (f âŠ” g) = toMultiset f âˆª toMultiset g := by
  ext
  -- âŠ¢ Multiset.count aâœ (â†‘toMultiset (f âŠ” g)) = Multiset.count aâœ (â†‘toMultiset f âˆª â€¦
  simp_rw [Multiset.count_union, Finsupp.count_toMultiset, Finsupp.sup_apply, sup_eq_max]
  -- ğŸ‰ no goals

theorem toMultiset_inf [DecidableEq Î±] (f g : Î± â†’â‚€ â„•) :
    toMultiset (f âŠ“ g) = toMultiset f âˆ© toMultiset g := by
  ext
  -- âŠ¢ Multiset.count aâœ (â†‘toMultiset (f âŠ“ g)) = Multiset.count aâœ (â†‘toMultiset f âˆ© â€¦
  simp_rw [Multiset.count_inter, Finsupp.count_toMultiset, Finsupp.inf_apply, inf_eq_min]
  -- ğŸ‰ no goals

@[simp]
theorem mem_toMultiset (f : Î± â†’â‚€ â„•) (i : Î±) : i âˆˆ toMultiset f â†” i âˆˆ f.support := by
  classical
  rw [â† Multiset.count_ne_zero, Finsupp.count_toMultiset, Finsupp.mem_support_iff]
#align finsupp.mem_to_multiset Finsupp.mem_toMultiset

end Finsupp

namespace Multiset

variable [DecidableEq Î±]

/-- Given a multiset `s`, `s.toFinsupp` returns the finitely supported function on `â„•` given by
the multiplicities of the elements of `s`. -/
@[simps symm_apply]
def toFinsupp : Multiset Î± â‰ƒ+ (Î± â†’â‚€ â„•) where
  toFun s := âŸ¨s.toFinset, fun a => s.count a, fun a => by simpâŸ©
                                                          -- ğŸ‰ no goals
  invFun f := Finsupp.toMultiset f
  map_add' s t := Finsupp.ext <| fun _ => count_add _ _ _
  right_inv f :=
    Finsupp.ext fun a => by
      simp only [Finsupp.toMultiset_apply, Finsupp.sum, Multiset.count_sum',
        Multiset.count_singleton, mul_boole, Finsupp.coe_mk, Finsupp.mem_support_iff,
        Multiset.count_nsmul, Finset.sum_ite_eq, ite_not, ite_eq_right_iff]
      exact Eq.symm
      -- ğŸ‰ no goals
  left_inv s := by simp only [Finsupp.toMultiset_apply, Finsupp.sum, Finsupp.coe_mk,
    Multiset.toFinset_sum_count_nsmul_eq]
#align multiset.to_finsupp Multiset.toFinsupp

@[simp]
theorem toFinsupp_support (s : Multiset Î±) : s.toFinsupp.support = s.toFinset := rfl
#align multiset.to_finsupp_support Multiset.toFinsupp_support

@[simp]
theorem toFinsupp_apply (s : Multiset Î±) (a : Î±) : toFinsupp s a = s.count a := rfl
#align multiset.to_finsupp_apply Multiset.toFinsupp_apply

theorem toFinsupp_zero : toFinsupp (0 : Multiset Î±) = 0 := _root_.map_zero _
#align multiset.to_finsupp_zero Multiset.toFinsupp_zero

theorem toFinsupp_add (s t : Multiset Î±) : toFinsupp (s + t) = toFinsupp s + toFinsupp t :=
  toFinsupp.map_add s t
#align multiset.to_finsupp_add Multiset.toFinsupp_add

@[simp]
theorem toFinsupp_singleton (a : Î±) : toFinsupp ({a} : Multiset Î±) = Finsupp.single a 1 :=
  by ext; rw [toFinsupp_apply, count_singleton, Finsupp.single_eq_pi_single, Pi.single_apply]
     -- âŠ¢ â†‘(â†‘toFinsupp {a}) aâœ = â†‘(Finsupp.single a 1) aâœ
          -- ğŸ‰ no goals
#align multiset.to_finsupp_singleton Multiset.toFinsupp_singleton

@[simp]
theorem toFinsupp_toMultiset (s : Multiset Î±) : Finsupp.toMultiset (toFinsupp s) = s :=
  Multiset.toFinsupp.symm_apply_apply s
#align multiset.to_finsupp_to_multiset Multiset.toFinsupp_toMultiset

theorem toFinsupp_eq_iff {s : Multiset Î±} {f : Î± â†’â‚€ â„•} :
    toFinsupp s = f â†” s = Finsupp.toMultiset f :=
  Multiset.toFinsupp.apply_eq_iff_symm_apply
#align multiset.to_finsupp_eq_iff Multiset.toFinsupp_eq_iff

theorem toFinsupp_union (s t : Multiset Î±) : toFinsupp (s âˆª t) = toFinsupp s âŠ” toFinsupp t := by
  ext
  -- âŠ¢ â†‘(â†‘toFinsupp (s âˆª t)) aâœ = â†‘(â†‘toFinsupp s âŠ” â†‘toFinsupp t) aâœ
  simp [sup_eq_max]
  -- ğŸ‰ no goals

theorem toFinsupp_inter (s t : Multiset Î±) : toFinsupp (s âˆ© t) = toFinsupp s âŠ“ toFinsupp t := by
  ext
  -- âŠ¢ â†‘(â†‘toFinsupp (s âˆ© t)) aâœ = â†‘(â†‘toFinsupp s âŠ“ â†‘toFinsupp t) aâœ
  simp [inf_eq_min]
  -- ğŸ‰ no goals

end Multiset

@[simp]
theorem Finsupp.toMultiset_toFinsupp [DecidableEq Î±] (f : Î± â†’â‚€ â„•) :
    Multiset.toFinsupp (Finsupp.toMultiset f) = f :=
  Multiset.toFinsupp.apply_symm_apply _
#align finsupp.to_multiset_to_finsupp Finsupp.toMultiset_toFinsupp

theorem Finsupp.toMultiset_eq_iff [DecidableEq Î±] {f : Î± â†’â‚€ â„•} {s : Multiset Î±}:
    Finsupp.toMultiset f = s â†” f = Multiset.toFinsupp s :=
  Multiset.toFinsupp.symm_apply_eq

/-! ### As an order isomorphism -/

namespace Finsupp
/-- `Finsupp.toMultiset` as an order isomorphism. -/
def orderIsoMultiset [DecidableEq Î¹] : (Î¹ â†’â‚€ â„•) â‰ƒo Multiset Î¹ where
  toEquiv := Multiset.toFinsupp.symm.toEquiv
  map_rel_iff' {f g} := by simp [le_def, Multiset.le_iff_count]
                           -- ğŸ‰ no goals
#align finsupp.order_iso_multiset Finsupp.orderIsoMultiset

@[simp]
theorem coe_orderIsoMultiset [DecidableEq Î¹] : â‡‘(@orderIsoMultiset Î¹ _) = toMultiset :=
  rfl
#align finsupp.coe_order_iso_multiset Finsupp.coe_orderIsoMultiset

@[simp]
theorem coe_orderIsoMultiset_symm [DecidableEq Î¹] :
  â‡‘(@orderIsoMultiset Î¹).symm = Multiset.toFinsupp :=
  rfl
#align finsupp.coe_order_iso_multiset_symm Finsupp.coe_orderIsoMultiset_symm

theorem toMultiset_strictMono : StrictMono (@toMultiset Î¹) :=
  by classical exact (@orderIsoMultiset Î¹ _).strictMono
     -- ğŸ‰ no goals
#align finsupp.to_multiset_strict_mono Finsupp.toMultiset_strictMono

theorem sum_id_lt_of_lt (m n : Î¹ â†’â‚€ â„•) (h : m < n) : (m.sum fun _ => id) < n.sum fun _ => id := by
  rw [â† card_toMultiset, â† card_toMultiset]
  -- âŠ¢ â†‘Multiset.card (â†‘toMultiset m) < â†‘Multiset.card (â†‘toMultiset n)
  apply Multiset.card_lt_of_lt
  -- âŠ¢ â†‘toMultiset m < â†‘toMultiset n
  exact toMultiset_strictMono h
  -- ğŸ‰ no goals
#align finsupp.sum_id_lt_of_lt Finsupp.sum_id_lt_of_lt

variable (Î¹)

/-- The order on `Î¹ â†’â‚€ â„•` is well-founded. -/
theorem lt_wf : WellFounded (@LT.lt (Î¹ â†’â‚€ â„•) _) :=
  Subrelation.wf (sum_id_lt_of_lt _ _) <| InvImage.wf _ Nat.lt_wfRel.2
#align finsupp.lt_wf Finsupp.lt_wf

-- TODO: generalize to `[WellFoundedRelation Î±] â†’ WellFoundedRelation (Î¹ â†’â‚€ Î±)`
instance : WellFoundedRelation (Î¹ â†’â‚€ â„•) where
  rel := (Â· < Â·)
  wf := lt_wf _

end Finsupp

theorem Multiset.toFinsupp_strictMono [DecidableEq Î¹] : StrictMono (@Multiset.toFinsupp Î¹ _) :=
  (@Finsupp.orderIsoMultiset Î¹).symm.strictMono
#align multiset.to_finsupp_strict_mono Multiset.toFinsupp_strictMono
