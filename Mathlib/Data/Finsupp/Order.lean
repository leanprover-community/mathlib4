/-
Copyright (c) 2021 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Aaron Anderson
-/
import Mathlib.Data.Finsupp.Defs

#align_import data.finsupp.order from "leanprover-community/mathlib"@"1d29de43a5ba4662dd33b5cfeecfc2a27a5a8a29"

/-!
# Pointwise order on finitely supported functions

This file lifts order structures on `Î±` to `Î¹ â†’â‚€ Î±`.

## Main declarations

* `Finsupp.orderEmbeddingToFun`: The order embedding from finitely supported functions to
  functions.
-/

-- porting notes: removed from module documentation because it moved to `data.finsupp.multiset`
-- TODO: move to `Data.Finsupp.Multiset` when that is ported
-- * `Finsupp.orderIsoMultiset`: The order isomorphism between `â„•`-valued finitely supported
--   functions and multisets.


noncomputable section

open BigOperators

open Finset

variable {Î¹ Î± : Type*}

namespace Finsupp

/-! ### Order structures -/


section Zero

variable [Zero Î±]

section LE

variable [LE Î±]

instance instLEFinsupp : LE (Î¹ â†’â‚€ Î±) :=
  âŸ¨fun f g => âˆ€ i, f i â‰¤ g iâŸ©

theorem le_def {f g : Î¹ â†’â‚€ Î±} : f â‰¤ g â†” âˆ€ i, f i â‰¤ g i :=
  Iff.rfl
#align finsupp.le_def Finsupp.le_def

/-- The order on `Finsupp`s over a partial order embeds into the order on functions -/
def orderEmbeddingToFun : (Î¹ â†’â‚€ Î±) â†ªo (Î¹ â†’ Î±) where
  toFun f := f
  inj' f g h :=
    Finsupp.ext fun i => by
      dsimp at h
      -- âŠ¢ â†‘f i = â†‘g i
      rw [h]
      -- ğŸ‰ no goals
  map_rel_iff' {a b} := (@le_def _ _ _ _ a b).symm
#align finsupp.order_embedding_to_fun Finsupp.orderEmbeddingToFun

@[simp]
theorem orderEmbeddingToFun_apply {f : Î¹ â†’â‚€ Î±} {i : Î¹} : orderEmbeddingToFun f i = f i :=
  rfl
#align finsupp.order_embedding_to_fun_apply Finsupp.orderEmbeddingToFun_apply

end LE

section Preorder

variable [Preorder Î±]

instance preorder : Preorder (Î¹ â†’â‚€ Î±) :=
  { Finsupp.instLEFinsupp with
    le_refl := fun f i => le_rfl
    le_trans := fun f g h hfg hgh i => (hfg i).trans (hgh i) }

theorem monotone_toFun : Monotone (Finsupp.toFun : (Î¹ â†’â‚€ Î±) â†’ Î¹ â†’ Î±) :=
    fun _f _g h a => le_def.1 h a
#align finsupp.monotone_to_fun Finsupp.monotone_toFun

end Preorder

instance partialorder [PartialOrder Î±] : PartialOrder (Î¹ â†’â‚€ Î±) :=
  { Finsupp.preorder with le_antisymm :=
      fun _f _g hfg hgf => ext fun i => (hfg i).antisymm (hgf i) }

instance semilatticeInf [SemilatticeInf Î±] : SemilatticeInf (Î¹ â†’â‚€ Î±) :=
  { Finsupp.partialorder with
    inf := zipWith (Â· âŠ“ Â·) inf_idem
    inf_le_left := fun _f _g _i => inf_le_left
    inf_le_right := fun _f _g _i => inf_le_right
    le_inf := fun _f _g _i h1 h2 s => le_inf (h1 s) (h2 s) }

@[simp]
theorem inf_apply [SemilatticeInf Î±] {i : Î¹} {f g : Î¹ â†’â‚€ Î±} : (f âŠ“ g) i = f i âŠ“ g i :=
  rfl
#align finsupp.inf_apply Finsupp.inf_apply

instance semilatticeSup [SemilatticeSup Î±] : SemilatticeSup (Î¹ â†’â‚€ Î±) :=
  { Finsupp.partialorder with
    sup := zipWith (Â· âŠ” Â·) sup_idem
    le_sup_left := fun _f _g _i => le_sup_left
    le_sup_right := fun _f _g _i => le_sup_right
    sup_le := fun _f _g _h hf hg i => sup_le (hf i) (hg i) }

@[simp]
theorem sup_apply [SemilatticeSup Î±] {i : Î¹} {f g : Î¹ â†’â‚€ Î±} : (f âŠ” g) i = f i âŠ” g i :=
  rfl
#align finsupp.sup_apply Finsupp.sup_apply

instance lattice [Lattice Î±] : Lattice (Î¹ â†’â‚€ Î±) :=
  { Finsupp.semilatticeInf, Finsupp.semilatticeSup with }
#align finsupp.lattice Finsupp.lattice

section Lattice
variable [DecidableEq Î¹] [Lattice Î±] (f g : Î¹ â†’â‚€ Î±)

theorem support_inf_union_support_sup : (f âŠ“ g).support âˆª (f âŠ” g).support = f.support âˆª g.support :=
  coe_injective <| compl_injective <| by ext; simp [inf_eq_and_sup_eq_iff]
                                         -- âŠ¢ xâœ âˆˆ (â†‘((f âŠ“ g).support âˆª (f âŠ” g).support))á¶œ â†” xâœ âˆˆ (â†‘(f.support âˆª g.support â€¦
                                              -- ğŸ‰ no goals
#align finsupp.support_inf_union_support_sup Finsupp.support_inf_union_support_sup

theorem support_sup_union_support_inf : (f âŠ” g).support âˆª (f âŠ“ g).support = f.support âˆª g.support :=
  (union_comm _ _).trans <| support_inf_union_support_sup _ _
#align finsupp.support_sup_union_support_inf Finsupp.support_sup_union_support_inf

end Lattice
end Zero

/-! ### Algebraic order structures -/


instance orderedAddCommMonoid [OrderedAddCommMonoid Î±] : OrderedAddCommMonoid (Î¹ â†’â‚€ Î±) :=
  { Finsupp.addCommMonoid, Finsupp.partialorder with
    add_le_add_left := fun _a _b h c s => add_le_add_left (h s) (c s) }

instance orderedCancelAddCommMonoid [OrderedCancelAddCommMonoid Î±] :
    OrderedCancelAddCommMonoid (Î¹ â†’â‚€ Î±) :=
  { Finsupp.orderedAddCommMonoid with
    le_of_add_le_add_left := fun _f _g _i h s => le_of_add_le_add_left (h s) }

instance contravariantClass [OrderedAddCommMonoid Î±] [ContravariantClass Î± Î± (Â· + Â·) (Â· â‰¤ Â·)] :
    ContravariantClass (Î¹ â†’â‚€ Î±) (Î¹ â†’â‚€ Î±) (Â· + Â·) (Â· â‰¤ Â·) :=
  âŸ¨fun _f _g _h H x => le_of_add_le_add_left <| H xâŸ©

section CanonicallyOrderedAddMonoid

variable [CanonicallyOrderedAddMonoid Î±]

instance orderBot : OrderBot (Î¹ â†’â‚€ Î±) where
  bot := 0
  bot_le := by simp only [le_def, coe_zero, Pi.zero_apply, imp_true_iff, zero_le]
               -- ğŸ‰ no goals

protected theorem bot_eq_zero : (âŠ¥ : Î¹ â†’â‚€ Î±) = 0 :=
  rfl
#align finsupp.bot_eq_zero Finsupp.bot_eq_zero

@[simp]
theorem add_eq_zero_iff (f g : Î¹ â†’â‚€ Î±) : f + g = 0 â†” f = 0 âˆ§ g = 0 := by
  simp [FunLike.ext_iff, forall_and]
  -- ğŸ‰ no goals
#align finsupp.add_eq_zero_iff Finsupp.add_eq_zero_iff

theorem le_iff' (f g : Î¹ â†’â‚€ Î±) {s : Finset Î¹} (hf : f.support âŠ† s) : f â‰¤ g â†” âˆ€ i âˆˆ s, f i â‰¤ g i :=
  âŸ¨fun h s _hs => h s, fun h s => by
    classical exact
        if H : s âˆˆ f.support then h s (hf H) else (not_mem_support_iff.1 H).symm â–¸ zero_le (g s)âŸ©
#align finsupp.le_iff' Finsupp.le_iff'

theorem le_iff (f g : Î¹ â†’â‚€ Î±) : f â‰¤ g â†” âˆ€ i âˆˆ f.support, f i â‰¤ g i :=
  le_iff' f g <| Subset.refl _
#align finsupp.le_iff Finsupp.le_iff

instance decidableLE [DecidableRel (@LE.le Î± _)] : DecidableRel (@LE.le (Î¹ â†’â‚€ Î±) _) := fun f g =>
  decidable_of_iff _ (le_iff f g).symm
#align finsupp.decidable_le Finsupp.decidableLE

@[simp]
theorem single_le_iff {i : Î¹} {x : Î±} {f : Î¹ â†’â‚€ Î±} : single i x â‰¤ f â†” x â‰¤ f i :=
  (le_iff' _ _ support_single_subset).trans <| by simp
                                                  -- ğŸ‰ no goals
#align finsupp.single_le_iff Finsupp.single_le_iff

variable [Sub Î±] [OrderedSub Î±] {f g : Î¹ â†’â‚€ Î±} {i : Î¹} {a b : Î±}

/-- This is called `tsub` for truncated subtraction, to distinguish it with subtraction in an
additive group. -/
instance tsub : Sub (Î¹ â†’â‚€ Î±) :=
  âŸ¨zipWith (fun m n => m - n) (tsub_self 0)âŸ©
#align finsupp.tsub Finsupp.tsub

instance orderedSub : OrderedSub (Î¹ â†’â‚€ Î±) :=
  âŸ¨fun _n _m _k => forall_congr' fun _x => tsub_le_iff_rightâŸ©

instance : CanonicallyOrderedAddMonoid (Î¹ â†’â‚€ Î±) :=
  { Finsupp.orderBot,
    Finsupp.orderedAddCommMonoid with
    exists_add_of_le := fun {f g} h => âŸ¨g - f, ext fun x => (add_tsub_cancel_of_le <| h x).symmâŸ©
    le_self_add := fun _f _g _x => le_self_add }

@[simp]
theorem coe_tsub (f g : Î¹ â†’â‚€ Î±) : â‡‘(f - g) = f - g :=
  rfl
#align finsupp.coe_tsub Finsupp.coe_tsub

theorem tsub_apply (f g : Î¹ â†’â‚€ Î±) (a : Î¹) : (f - g) a = f a - g a :=
  rfl
#align finsupp.tsub_apply Finsupp.tsub_apply

@[simp]
theorem single_tsub : single i (a - b) = single i a - single i b := by
  ext j
  -- âŠ¢ â†‘(single i (a - b)) j = â†‘(single i a - single i b) j
  obtain rfl | h := eq_or_ne i j
  -- âŠ¢ â†‘(single i (a - b)) i = â†‘(single i a - single i b) i
  Â· rw [tsub_apply, single_eq_same, single_eq_same, single_eq_same]
    -- ğŸ‰ no goals
  Â· rw [tsub_apply, single_eq_of_ne h, single_eq_of_ne h, single_eq_of_ne h, tsub_self]
    -- ğŸ‰ no goals
#align finsupp.single_tsub Finsupp.single_tsub

theorem support_tsub {f1 f2 : Î¹ â†’â‚€ Î±} : (f1 - f2).support âŠ† f1.support := by
  simp (config := { contextual := true }) only [subset_iff, tsub_eq_zero_iff_le, mem_support_iff,
    Ne.def, coe_tsub, Pi.sub_apply, not_imp_not, zero_le, imp_true_iff]
#align finsupp.support_tsub Finsupp.support_tsub

theorem subset_support_tsub [DecidableEq Î¹] {f1 f2 : Î¹ â†’â‚€ Î±} :
    f1.support \ f2.support âŠ† (f1 - f2).support := by
  simp (config := { contextual := true }) [subset_iff]
  -- ğŸ‰ no goals
#align finsupp.subset_support_tsub Finsupp.subset_support_tsub

end CanonicallyOrderedAddMonoid

section CanonicallyLinearOrderedAddMonoid

variable [CanonicallyLinearOrderedAddMonoid Î±]

@[simp]
theorem support_inf [DecidableEq Î¹] (f g : Î¹ â†’â‚€ Î±) : (f âŠ“ g).support = f.support âˆ© g.support := by
  ext
  -- âŠ¢ aâœ âˆˆ (f âŠ“ g).support â†” aâœ âˆˆ f.support âˆ© g.support
  simp only [inf_apply, mem_support_iff, Ne.def, Finset.mem_union, Finset.mem_filter,
    Finset.mem_inter]
  simp only [inf_eq_min, â† nonpos_iff_eq_zero, min_le_iff, not_or]
  -- ğŸ‰ no goals
#align finsupp.support_inf Finsupp.support_inf

@[simp]
theorem support_sup [DecidableEq Î¹] (f g : Î¹ â†’â‚€ Î±) : (f âŠ” g).support = f.support âˆª g.support := by
  ext
  -- âŠ¢ aâœ âˆˆ (f âŠ” g).support â†” aâœ âˆˆ f.support âˆª g.support
  simp only [Finset.mem_union, mem_support_iff, sup_apply, Ne.def, â† bot_eq_zero]
  -- âŠ¢ Â¬â†‘f aâœ âŠ” â†‘g aâœ = âŠ¥ â†” Â¬â†‘f aâœ = âŠ¥ âˆ¨ Â¬â†‘g aâœ = âŠ¥
  rw [_root_.sup_eq_bot_iff, not_and_or]
  -- ğŸ‰ no goals
#align finsupp.support_sup Finsupp.support_sup

nonrec theorem disjoint_iff {f g : Î¹ â†’â‚€ Î±} : Disjoint f g â†” Disjoint f.support g.support := by
  classical
    rw [disjoint_iff, disjoint_iff, Finsupp.bot_eq_zero, â† Finsupp.support_eq_empty,
      Finsupp.support_inf]
    rfl
#align finsupp.disjoint_iff Finsupp.disjoint_iff

end CanonicallyLinearOrderedAddMonoid

/-! ### Some lemmas about `â„•` -/


section Nat

theorem sub_single_one_add {a : Î¹} {u u' : Î¹ â†’â‚€ â„•} (h : u a â‰  0) :
    u - single a 1 + u' = u + u' - single a 1 :=
  tsub_add_eq_add_tsub <| single_le_iff.mpr <| Nat.one_le_iff_ne_zero.mpr h
#align finsupp.sub_single_one_add Finsupp.sub_single_one_add

theorem add_sub_single_one {a : Î¹} {u u' : Î¹ â†’â‚€ â„•} (h : u' a â‰  0) :
    u + (u' - single a 1) = u + u' - single a 1 :=
  (add_tsub_assoc_of_le (single_le_iff.mpr <| Nat.one_le_iff_ne_zero.mpr h) _).symm
#align finsupp.add_sub_single_one Finsupp.add_sub_single_one

end Nat

end Finsupp
