/-
Copyright (c) 2020 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro
-/
import Mathlib.Data.Num.Lemmas
import Mathlib.Data.Nat.Prime
import Mathlib.Tactic.Ring

#align_import data.num.prime from "leanprover-community/mathlib"@"58581d0fe523063f5651df0619be2bf65012a94a"

/-!
# Primality for binary natural numbers

This file defines versions of `Nat.minFac` and `Nat.Prime` for `Num` and `PosNum`. As with other
`Num` definitions, they are not intended for general use (`Nat` should be used instead of `Num` in
most cases) but they can be used in contexts where kernel computation is required, such as proofs
by `rfl` and `decide`, as well as in `#reduce`.

The default decidable instance for `Nat.Prime` is optimized for VM evaluation, so it should be
preferred within `#eval` or in tactic execution, while for proofs the `norm_num` tactic can be used
to construct primality and non-primality proofs more efficiently than kernel computation.

Nevertheless, sometimes proof by computational reflection requires natural number computations, and
`Num` implements algorithms directly on binary natural numbers for this purpose.
-/


namespace PosNum

/-- Auxiliary function for computing the smallest prime factor of a `PosNum`. Unlike
`Nat.minFacAux`, we use a natural number `fuel` variable that is set to an upper bound on the
number of iterations. It is initialized to the number `n` we are determining primality for. Even
though this is exponential in the input (since it is a `Nat`, not a `Num`), it will get lazily
evaluated during kernel reduction, so we will only require about `sqrt n` unfoldings, for the
`sqrt n` iterations of the loop. -/
def minFacAux (n : PosNum) : â„• â†’ PosNum â†’ PosNum
  | 0, _ => n
  | fuel + 1, k =>
    if n < k.bit1 * k.bit1 then n else if k.bit1 âˆ£ n then k.bit1 else minFacAux n fuel k.succ
#align pos_num.min_fac_aux PosNum.minFacAux

set_option linter.deprecated false in
theorem minFacAux_to_nat {fuel : â„•} {n k : PosNum} (h : Nat.sqrt n < fuel + k.bit1) :
    (minFacAux n fuel k : â„•) = Nat.minFacAux n k.bit1 := by
  induction' fuel with fuel ih generalizing k <;> rw [minFacAux, Nat.minFacAux]
  -- âŠ¢ â†‘(minFacAux n Nat.zero k) = Nat.minFacAux â†‘n â†‘(bit1 k)
                                                  -- âŠ¢ â†‘n =
                                                  -- âŠ¢ â†‘(if n < bit1 k * bit1 k then n else if bit1 k âˆ£ n then bit1 k else minFacAu â€¦
  Â· rw [Nat.zero_add, Nat.sqrt_lt] at h
    -- âŠ¢ â†‘n =
    simp only [h, dite_true]
    -- ðŸŽ‰ no goals
  simp_rw [â† mul_to_nat]
  -- âŠ¢ â†‘(if n < bit1 k * bit1 k then n else if bit1 k âˆ£ n then bit1 k else minFacAu â€¦
  simp only [cast_lt, dvd_to_nat]
  -- âŠ¢ â†‘(if n < bit1 k * bit1 k then n else if bit1 k âˆ£ n then bit1 k else minFacAu â€¦
  split_ifs <;> try rfl
                -- ðŸŽ‰ no goals
                -- ðŸŽ‰ no goals
                -- âŠ¢ â†‘(minFacAux n fuel (succ k)) = Nat.minFacAux (â†‘n) (â†‘(bit1 k) + 2)
  rw [ih] <;> [congr; convert Nat.lt_succ_of_lt h using 1] <;>
  -- âŠ¢ â†‘(bit1 (succ k)) = â†‘(bit1 k) + 2
    simp only [_root_.bit1, _root_.bit0, cast_bit1, cast_succ, Nat.succ_eq_add_one, add_assoc,
      add_left_comm, â† one_add_one_eq_two]
#align pos_num.min_fac_aux_to_nat PosNum.minFacAux_to_nat

/-- Returns the smallest prime factor of `n â‰  1`. -/
def minFac : PosNum â†’ PosNum
  | 1 => 1
  | bit0 _ => 2
  | bit1 n => minFacAux (bit1 n) n 1
#align pos_num.min_fac PosNum.minFac

@[simp]
theorem minFac_to_nat (n : PosNum) : (minFac n : â„•) = Nat.minFac n := by
  cases' n with n
  Â· rfl
    -- ðŸŽ‰ no goals
  Â· rw [minFac, Nat.minFac_eq, if_neg]
    -- âŠ¢ â†‘(match bit1 n with
    swap
    Â· simp
      -- ðŸŽ‰ no goals
    rw [minFacAux_to_nat]
    -- âŠ¢ Nat.minFacAux â†‘(bit1 n) â†‘(bit1 1) = Nat.minFacAux (â†‘(bit1 n)) 3
    Â· rfl
      -- ðŸŽ‰ no goals
    simp only [cast_one, cast_bit1]
    -- âŠ¢ Nat.sqrt (_root_.bit1 â†‘n) < â†‘n + _root_.bit1 1
    unfold _root_.bit1 _root_.bit0
    -- âŠ¢ Nat.sqrt (â†‘n + â†‘n + 1) < â†‘n + (1 + 1 + 1)
    rw [Nat.sqrt_lt]
    -- âŠ¢ â†‘n + â†‘n + 1 < (â†‘n + (1 + 1 + 1)) * (â†‘n + (1 + 1 + 1))
    calc
      (n : â„•) + (n : â„•) + 1 â‰¤ (n : â„•) + (n : â„•) + (n : â„•) := by simp
      _ = (n : â„•) * (1 + 1 + 1) := by simp only [mul_add, mul_one]
      _ < _ := by simp [mul_lt_mul]
  Â· rw [minFac, Nat.minFac_eq, if_pos]
    -- âŠ¢ â†‘(match bit0 aâœ with
    Â· rfl
      -- ðŸŽ‰ no goals
    simp
    -- ðŸŽ‰ no goals
#align pos_num.min_fac_to_nat PosNum.minFac_to_nat

/-- Primality predicate for a `PosNum`. -/
@[simp]
def Prime (n : PosNum) : Prop :=
  Nat.Prime n
#align pos_num.prime PosNum.Prime

instance decidablePrime : DecidablePred PosNum.Prime
  | 1 => Decidable.isFalse Nat.not_prime_one
  | bit0 n =>
    decidable_of_iff' (n = 1)
      (by
        refine' Nat.prime_def_minFac.trans ((and_iff_right _).trans <| eq_comm.trans _)
        -- âŠ¢ 2 â‰¤ â†‘(bit0 n)
        Â· exact bit0_le_bit0.2 (Nat.succ_le_of_lt (to_nat_pos _))
          -- ðŸŽ‰ no goals
        rw [â† minFac_to_nat, to_nat_inj]
        -- âŠ¢ bit0 n = minFac (bit0 n) â†” n = 1
        exact âŸ¨bit0.inj, congr_arg _âŸ©)
        -- ðŸŽ‰ no goals
  | bit1 n =>
    decidable_of_iff' (minFacAux (bit1 n) n 1 = bit1 n)
      (by
        refine' Nat.prime_def_minFac.trans ((and_iff_right _).trans _)
        -- âŠ¢ 2 â‰¤ â†‘(bit1 n)
        Â· exact Nat.bit0_le_bit1_iff.2 (to_nat_pos _)
          -- ðŸŽ‰ no goals
        rw [â† minFac_to_nat, to_nat_inj]; rfl)
        -- âŠ¢ minFac (bit1 n) = bit1 n â†” minFacAux (bit1 n) (â†‘n) 1 = bit1 n
                                          -- ðŸŽ‰ no goals
#align pos_num.decidable_prime PosNum.decidablePrime

end PosNum

namespace Num

/-- Returns the smallest prime factor of `n â‰  1`. -/
def minFac : Num â†’ PosNum
  | 0 => 2
  | pos n => n.minFac
#align num.min_fac Num.minFac

@[simp]
theorem minFac_to_nat : âˆ€ n : Num, (minFac n : â„•) = Nat.minFac n
  | 0 => rfl
  | pos _ => PosNum.minFac_to_nat _
#align num.min_fac_to_nat Num.minFac_to_nat

/-- Primality predicate for a `Num`. -/
@[simp]
def Prime (n : Num) : Prop :=
  Nat.Prime n
#align num.prime Num.Prime

instance decidablePrime : DecidablePred Num.Prime
  | 0 => Decidable.isFalse Nat.not_prime_zero
  | pos n => PosNum.decidablePrime n
#align num.decidable_prime Num.decidablePrime

end Num
