/-
Copyright (c) 2019 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro
-/
import Mathlib.Data.Rat.Order
import Mathlib.Data.Rat.Lemmas
import Mathlib.Data.Int.CharZero
import Mathlib.Algebra.GroupWithZero.Power
import Mathlib.Algebra.Field.Opposite
import Mathlib.Algebra.Order.Field.Basic

#align_import data.rat.cast from "leanprover-community/mathlib"@"acebd8d49928f6ed8920e502a6c90674e75bd441"


/-!
# Casts for Rational Numbers

## Summary

We define the canonical injection from ‚Ñö into an arbitrary division ring and prove various
casting lemmas showing the well-behavedness of this injection.

## Notations

- `/.` is infix notation for `rat.mk`.

## Tags

rat, rationals, field, ‚Ñö, numerator, denominator, num, denom, cast, coercion, casting
-/


variable {F Œπ Œ± Œ≤ : Type*}

namespace Rat

open Rat

section WithDivRing

variable [DivisionRing Œ±]

@[simp, norm_cast]
theorem cast_coe_int (n : ‚Ñ§) : ((n : ‚Ñö) : Œ±) = n :=
  (cast_def _).trans <| show (n / (1 : ‚Ñï) : Œ±) = n by rw [Nat.cast_one, div_one]
                                                      -- üéâ no goals
#align rat.cast_coe_int Rat.cast_coe_int


@[simp, norm_cast]
theorem cast_coe_nat (n : ‚Ñï) : ((n : ‚Ñö) : Œ±) = n := by
  rw [‚Üê Int.cast_ofNat, cast_coe_int, Int.cast_ofNat]
  -- üéâ no goals
#align rat.cast_coe_nat Rat.cast_coe_nat


@[simp, norm_cast]
theorem cast_zero : ((0 : ‚Ñö) : Œ±) = 0 :=
  (cast_coe_int _).trans Int.cast_zero
#align rat.cast_zero Rat.cast_zero

@[simp, norm_cast]
theorem cast_one : ((1 : ‚Ñö) : Œ±) = 1 :=
  (cast_coe_int _).trans Int.cast_one
#align rat.cast_one Rat.cast_one

theorem cast_commute (r : ‚Ñö) (a : Œ±) : Commute (‚Üër) a := by
  simpa only [cast_def] using (r.1.cast_commute a).div_left (r.2.cast_commute a)
  -- üéâ no goals
#align rat.cast_commute Rat.cast_commute

theorem cast_comm (r : ‚Ñö) (a : Œ±) : (r : Œ±) * a = a * r :=
  (cast_commute r a).eq
#align rat.cast_comm Rat.cast_comm

theorem commute_cast (a : Œ±) (r : ‚Ñö) : Commute a r :=
  (r.cast_commute a).symm
#align rat.commute_cast Rat.commute_cast

@[norm_cast]
theorem cast_mk_of_ne_zero (a b : ‚Ñ§) (b0 : (b : Œ±) ‚â† 0) : (a /. b : Œ±) = a / b := by
  have b0' : b ‚â† 0 := by
    refine' mt _ b0
    simp (config := { contextual := true })
  cases' e : a /. b with n d h c
  -- ‚ä¢ ‚Üë(mk' n d) = ‚Üëa / ‚Üëb
  have d0 : (d : Œ±) ‚â† 0 := by
    intro d0
    have dd := den_dvd a b
    cases' show (d : ‚Ñ§) ‚à£ b by rwa [e] at dd with k ke
    have : (b : Œ±) = (d : Œ±) * (k : Œ±) := by rw [ke, Int.cast_mul, Int.cast_ofNat]
    rw [d0, zero_mul] at this
    contradiction
  rw [num_den'] at e
  -- ‚ä¢ ‚Üë(mk' n d) = ‚Üëa / ‚Üëb
  have := congr_arg ((‚Üë) : ‚Ñ§ ‚Üí Œ±)
    ((divInt_eq_iff b0' <| ne_of_gt <| Int.coe_nat_pos.2 h.bot_lt).1 e)
  rw [Int.cast_mul, Int.cast_mul, Int.cast_ofNat] at this
  -- ‚ä¢ ‚Üë(mk' n d) = ‚Üëa / ‚Üëb
  -- Porting note: was `symm`
  apply Eq.symm
  -- ‚ä¢ ‚Üëa / ‚Üëb = ‚Üë(mk' n d)
  rw [cast_def, div_eq_mul_inv, eq_div_iff_mul_eq d0, mul_assoc, (d.commute_cast _).eq, ‚Üê mul_assoc,
    this, mul_assoc, mul_inv_cancel b0, mul_one]
#align rat.cast_mk_of_ne_zero Rat.cast_mk_of_ne_zero

@[norm_cast]
theorem cast_add_of_ne_zero :
    ‚àÄ {m n : ‚Ñö}, (m.den : Œ±) ‚â† 0 ‚Üí (n.den : Œ±) ‚â† 0 ‚Üí ((m + n : ‚Ñö) : Œ±) = m + n
  | ‚ü®n‚ÇÅ, d‚ÇÅ, h‚ÇÅ, c‚ÇÅ‚ü©, ‚ü®n‚ÇÇ, d‚ÇÇ, h‚ÇÇ, c‚ÇÇ‚ü© => fun (d‚ÇÅ0 : (d‚ÇÅ : Œ±) ‚â† 0) (d‚ÇÇ0 : (d‚ÇÇ : Œ±) ‚â† 0) => by
    have d‚ÇÅ0' : (d‚ÇÅ : ‚Ñ§) ‚â† 0 :=
      Int.coe_nat_ne_zero.2 fun e => by rw [e] at d‚ÇÅ0; exact d‚ÇÅ0 Nat.cast_zero
    have d‚ÇÇ0' : (d‚ÇÇ : ‚Ñ§) ‚â† 0 :=
      Int.coe_nat_ne_zero.2 fun e => by rw [e] at d‚ÇÇ0; exact d‚ÇÇ0 Nat.cast_zero
    rw [num_den', num_den', add_def'' d‚ÇÅ0' d‚ÇÇ0']
    -- ‚ä¢ ‚Üë((n‚ÇÅ * ‚Üëd‚ÇÇ + n‚ÇÇ * ‚Üëd‚ÇÅ) /. (‚Üëd‚ÇÅ * ‚Üëd‚ÇÇ)) = ‚Üë(n‚ÇÅ /. ‚Üëd‚ÇÅ) + ‚Üë(n‚ÇÇ /. ‚Üëd‚ÇÇ)
    suffices (n‚ÇÅ * (d‚ÇÇ * ((d‚ÇÇ : Œ±)‚Åª¬π * (d‚ÇÅ : Œ±)‚Åª¬π)) + n‚ÇÇ * (d‚ÇÅ * (d‚ÇÇ : Œ±)‚Åª¬π) * (d‚ÇÅ : Œ±)‚Åª¬π : Œ±)
        = n‚ÇÅ * (d‚ÇÅ : Œ±)‚Åª¬π + n‚ÇÇ * (d‚ÇÇ : Œ±)‚Åª¬π by
      rw [cast_mk_of_ne_zero, cast_mk_of_ne_zero, cast_mk_of_ne_zero]
      ¬∑ simpa [division_def, left_distrib, right_distrib, mul_inv_rev, d‚ÇÅ0, d‚ÇÇ0, mul_assoc]
      all_goals simp [d‚ÇÅ0, d‚ÇÇ0]
    rw [‚Üê mul_assoc (d‚ÇÇ : Œ±), mul_inv_cancel d‚ÇÇ0, one_mul, (Nat.cast_commute _ _).eq]
    -- ‚ä¢ ‚Üën‚ÇÅ * (‚Üëd‚ÇÅ)‚Åª¬π + ‚Üën‚ÇÇ * ((‚Üëd‚ÇÇ)‚Åª¬π * ‚Üëd‚ÇÅ) * (‚Üëd‚ÇÅ)‚Åª¬π = ‚Üën‚ÇÅ * (‚Üëd‚ÇÅ)‚Åª¬π + ‚Üën‚ÇÇ * (‚Üëd‚ÇÇ ‚Ä¶
    simp [d‚ÇÅ0, mul_assoc]
    -- üéâ no goals
#align rat.cast_add_of_ne_zero Rat.cast_add_of_ne_zero

@[simp, norm_cast]
theorem cast_neg : ‚àÄ n, ((-n : ‚Ñö) : Œ±) = -n
  | ‚ü®n, d, h, c‚ü© => by
    simpa only [cast_def] using
      show (‚Üë(-n) / d : Œ±) = -(n / d) by
        rw [div_eq_mul_inv, div_eq_mul_inv, Int.cast_neg, neg_mul_eq_neg_mul]
#align rat.cast_neg Rat.cast_neg

@[norm_cast]
theorem cast_sub_of_ne_zero {m n : ‚Ñö} (m0 : (m.den : Œ±) ‚â† 0) (n0 : (n.den : Œ±) ‚â† 0) :
    ((m - n : ‚Ñö) : Œ±) = m - n := by
  have : ((-n).den : Œ±) ‚â† 0 := by cases n; exact n0
  -- ‚ä¢ ‚Üë(m - n) = ‚Üëm - ‚Üën
  simp [sub_eq_add_neg, cast_add_of_ne_zero m0 this]
  -- üéâ no goals
#align rat.cast_sub_of_ne_zero Rat.cast_sub_of_ne_zero

@[norm_cast]
theorem cast_mul_of_ne_zero :
    ‚àÄ {m n : ‚Ñö}, (m.den : Œ±) ‚â† 0 ‚Üí (n.den : Œ±) ‚â† 0 ‚Üí ((m * n : ‚Ñö) : Œ±) = m * n
  | ‚ü®n‚ÇÅ, d‚ÇÅ, h‚ÇÅ, c‚ÇÅ‚ü©, ‚ü®n‚ÇÇ, d‚ÇÇ, h‚ÇÇ, c‚ÇÇ‚ü© => fun (d‚ÇÅ0 : (d‚ÇÅ : Œ±) ‚â† 0) (d‚ÇÇ0 : (d‚ÇÇ : Œ±) ‚â† 0) => by
    have d‚ÇÅ0' : (d‚ÇÅ : ‚Ñ§) ‚â† 0 :=
      Int.coe_nat_ne_zero.2 fun e => by rw [e] at d‚ÇÅ0; exact d‚ÇÅ0 Nat.cast_zero
    have d‚ÇÇ0' : (d‚ÇÇ : ‚Ñ§) ‚â† 0 :=
      Int.coe_nat_ne_zero.2 fun e => by rw [e] at d‚ÇÇ0; exact d‚ÇÇ0 Nat.cast_zero
    rw [num_den', num_den', mul_def' d‚ÇÅ0' d‚ÇÇ0']
    -- ‚ä¢ ‚Üë(n‚ÇÅ * n‚ÇÇ /. (‚Üëd‚ÇÅ * ‚Üëd‚ÇÇ)) = ‚Üë(n‚ÇÅ /. ‚Üëd‚ÇÅ) * ‚Üë(n‚ÇÇ /. ‚Üëd‚ÇÇ)
    suffices (n‚ÇÅ * (n‚ÇÇ * (d‚ÇÇ : Œ±)‚Åª¬π * (d‚ÇÅ : Œ±)‚Åª¬π) : Œ±) = n‚ÇÅ * ((d‚ÇÅ : Œ±)‚Åª¬π * (n‚ÇÇ * (d‚ÇÇ : Œ±)‚Åª¬π)) by
      rw [cast_mk_of_ne_zero, cast_mk_of_ne_zero, cast_mk_of_ne_zero]
      ¬∑ simpa [division_def, mul_inv_rev, d‚ÇÅ0, d‚ÇÇ0, mul_assoc]
      all_goals simp [d‚ÇÅ0, d‚ÇÇ0]
    rw [(d‚ÇÅ.commute_cast (_ : Œ±)).inv_right‚ÇÄ.eq]
    -- üéâ no goals
#align rat.cast_mul_of_ne_zero Rat.cast_mul_of_ne_zero

-- Porting note: rewrote proof
@[simp]
theorem cast_inv_nat (n : ‚Ñï) : ((n‚Åª¬π : ‚Ñö) : Œ±) = (n : Œ±)‚Åª¬π := by
  cases' n with n
  -- ‚ä¢ ‚Üë(‚ÜëNat.zero)‚Åª¬π = (‚ÜëNat.zero)‚Åª¬π
  ¬∑ simp
    -- üéâ no goals
  rw [cast_def, inv_coe_nat_num, inv_coe_nat_den, if_neg n.succ_ne_zero,
    Int.sign_eq_one_of_pos (Nat.cast_pos.mpr n.succ_pos), Int.cast_one, one_div]
#align rat.cast_inv_nat Rat.cast_inv_nat

-- Porting note: proof got a lot easier - is this still the intended statement?
@[simp]
theorem cast_inv_int (n : ‚Ñ§) : ((n‚Åª¬π : ‚Ñö) : Œ±) = (n : Œ±)‚Åª¬π := by
  cases' n with n n
  -- ‚ä¢ ‚Üë(‚Üë(Int.ofNat n))‚Åª¬π = (‚Üë(Int.ofNat n))‚Åª¬π
  ¬∑ simp [ofInt_eq_cast, cast_inv_nat]
    -- üéâ no goals
  ¬∑ simp only [ofInt_eq_cast, Int.cast_negSucc, ‚Üê Nat.cast_succ, cast_neg, inv_neg, cast_inv_nat]
    -- üéâ no goals
#align rat.cast_inv_int Rat.cast_inv_int

@[norm_cast]
theorem cast_inv_of_ne_zero :
  ‚àÄ {n : ‚Ñö}, (n.num : Œ±) ‚â† 0 ‚Üí (n.den : Œ±) ‚â† 0 ‚Üí ((n‚Åª¬π : ‚Ñö) : Œ±) = (n : Œ±)‚Åª¬π
  | ‚ü®n, d, h, c‚ü© => fun (n0 : (n : Œ±) ‚â† 0) (d0 : (d : Œ±) ‚â† 0) => by
    have _ : (n : ‚Ñ§) ‚â† 0 := fun e => by rw [e] at n0; exact n0 Int.cast_zero
    -- ‚ä¢ ‚Üë(mk' n d)‚Åª¬π = (‚Üë(mk' n d))‚Åª¬π
    have _ : (d : ‚Ñ§) ‚â† 0 :=
      Int.coe_nat_ne_zero.2 fun e => by rw [e] at d0; exact d0 Nat.cast_zero
    rw [num_den', inv_def']
    -- ‚ä¢ ‚Üë(‚Üëd /. n) = (‚Üë(n /. ‚Üëd))‚Åª¬π
    rw [cast_mk_of_ne_zero, cast_mk_of_ne_zero, inv_div] <;> simp [n0, d0]
    -- ‚ä¢ ‚Üë‚Üëd ‚â† 0
                                                             -- üéâ no goals
                                                             -- üéâ no goals
#align rat.cast_inv_of_ne_zero Rat.cast_inv_of_ne_zero

@[norm_cast]
theorem cast_div_of_ne_zero {m n : ‚Ñö} (md : (m.den : Œ±) ‚â† 0) (nn : (n.num : Œ±) ‚â† 0)
    (nd : (n.den : Œ±) ‚â† 0) : ((m / n : ‚Ñö) : Œ±) = m / n := by
  have : (n‚Åª¬π.den : ‚Ñ§) ‚à£ n.num := by
    conv in n‚Åª¬π.den => rw [‚Üê @num_den n, inv_def']
    apply den_dvd
  have : (n‚Åª¬π.den : Œ±) = 0 ‚Üí (n.num : Œ±) = 0 := fun h => by
    let ‚ü®k, e‚ü© := this
    have := congr_arg ((‚Üë) : ‚Ñ§ ‚Üí Œ±) e; rwa [Int.cast_mul, Int.cast_ofNat, h, zero_mul] at this
  rw [division_def, cast_mul_of_ne_zero md (mt this nn), cast_inv_of_ne_zero nn nd, division_def]
  -- üéâ no goals
#align rat.cast_div_of_ne_zero Rat.cast_div_of_ne_zero

@[simp, norm_cast]
theorem cast_inj [CharZero Œ±] : ‚àÄ {m n : ‚Ñö}, (m : Œ±) = n ‚Üî m = n
  | ‚ü®n‚ÇÅ, d‚ÇÅ, d‚ÇÅ0, c‚ÇÅ‚ü©, ‚ü®n‚ÇÇ, d‚ÇÇ, d‚ÇÇ0, c‚ÇÇ‚ü© => by
    refine' ‚ü®fun h => _, congr_arg _‚ü©
    -- ‚ä¢ mk' n‚ÇÅ d‚ÇÅ = mk' n‚ÇÇ d‚ÇÇ
    have d‚ÇÅa : (d‚ÇÅ : Œ±) ‚â† 0 := Nat.cast_ne_zero.2 d‚ÇÅ0
    -- ‚ä¢ mk' n‚ÇÅ d‚ÇÅ = mk' n‚ÇÇ d‚ÇÇ
    have d‚ÇÇa : (d‚ÇÇ : Œ±) ‚â† 0 := Nat.cast_ne_zero.2 d‚ÇÇ0
    -- ‚ä¢ mk' n‚ÇÅ d‚ÇÅ = mk' n‚ÇÇ d‚ÇÇ
    rw [num_den', num_den'] at h ‚ä¢
    -- ‚ä¢ n‚ÇÅ /. ‚Üëd‚ÇÅ = n‚ÇÇ /. ‚Üëd‚ÇÇ
    rw [cast_mk_of_ne_zero, cast_mk_of_ne_zero] at h <;> simp [d‚ÇÅ0, d‚ÇÇ0] at h ‚ä¢
                                                         -- ‚ä¢ mkRat n‚ÇÅ d‚ÇÅ = mkRat n‚ÇÇ d‚ÇÇ
                                                         -- üéâ no goals
                                                         -- üéâ no goals
    rwa [eq_div_iff_mul_eq d‚ÇÇa, division_def, mul_assoc, (d‚ÇÅ.cast_commute (d‚ÇÇ : Œ±)).inv_left‚ÇÄ.eq, ‚Üê
      mul_assoc, ‚Üê division_def, eq_comm, eq_div_iff_mul_eq d‚ÇÅa, eq_comm, ‚Üê Int.cast_ofNat d‚ÇÅ, ‚Üê
      Int.cast_mul, ‚Üê Int.cast_ofNat d‚ÇÇ, ‚Üê Int.cast_mul, Int.cast_inj, ‚Üê mkRat_eq_iff d‚ÇÅ0 d‚ÇÇ0] at h
#align rat.cast_inj Rat.cast_inj

theorem cast_injective [CharZero Œ±] : Function.Injective ((‚Üë) : ‚Ñö ‚Üí Œ±)
  | _, _ => cast_inj.1
#align rat.cast_injective Rat.cast_injective

@[simp]
theorem cast_eq_zero [CharZero Œ±] {n : ‚Ñö} : (n : Œ±) = 0 ‚Üî n = 0 := by rw [‚Üê cast_zero, cast_inj]
                                                                      -- üéâ no goals
#align rat.cast_eq_zero Rat.cast_eq_zero

theorem cast_ne_zero [CharZero Œ±] {n : ‚Ñö} : (n : Œ±) ‚â† 0 ‚Üî n ‚â† 0 :=
  not_congr cast_eq_zero
#align rat.cast_ne_zero Rat.cast_ne_zero

@[simp, norm_cast]
theorem cast_add [CharZero Œ±] (m n) : ((m + n : ‚Ñö) : Œ±) = m + n :=
  cast_add_of_ne_zero (Nat.cast_ne_zero.2 <| ne_of_gt m.pos) (Nat.cast_ne_zero.2 <| ne_of_gt n.pos)
#align rat.cast_add Rat.cast_add

@[simp, norm_cast]
theorem cast_sub [CharZero Œ±] (m n) : ((m - n : ‚Ñö) : Œ±) = m - n :=
  cast_sub_of_ne_zero (Nat.cast_ne_zero.2 <| ne_of_gt m.pos) (Nat.cast_ne_zero.2 <| ne_of_gt n.pos)
#align rat.cast_sub Rat.cast_sub

@[simp, norm_cast]
theorem cast_mul [CharZero Œ±] (m n) : ((m * n : ‚Ñö) : Œ±) = m * n :=
  cast_mul_of_ne_zero (Nat.cast_ne_zero.2 <| ne_of_gt m.pos) (Nat.cast_ne_zero.2 <| ne_of_gt n.pos)
#align rat.cast_mul Rat.cast_mul

section

set_option linter.deprecated false

@[simp, norm_cast]
theorem cast_bit0 [CharZero Œ±] (n : ‚Ñö) : ((bit0 n : ‚Ñö) : Œ±) = (bit0 n : Œ±) :=
  cast_add _ _
#align rat.cast_bit0 Rat.cast_bit0

@[simp, norm_cast]
theorem cast_bit1 [CharZero Œ±] (n : ‚Ñö) : ((bit1 n : ‚Ñö) : Œ±) = (bit1 n : Œ±) := by
  rw [bit1, cast_add, cast_one, cast_bit0]; rfl
  -- ‚ä¢ bit0 ‚Üën + 1 = bit1 ‚Üën
                                            -- üéâ no goals
#align rat.cast_bit1 Rat.cast_bit1

end

variable (Œ±)
variable [CharZero Œ±]

/-- Coercion `‚Ñö ‚Üí Œ±` as a `RingHom`. -/
def castHom : ‚Ñö ‚Üí+* Œ± where
  toFun := (‚Üë)
  map_one' := cast_one
  map_mul' := cast_mul
  map_zero' := cast_zero
  map_add' := cast_add
#align rat.cast_hom Rat.castHom

variable {Œ±}

@[simp]
theorem coe_cast_hom : ‚áë(castHom Œ±) = ((‚Üë) : ‚Ñö ‚Üí Œ±) :=
  rfl
#align rat.coe_cast_hom Rat.coe_cast_hom

@[simp, norm_cast]
theorem cast_inv (n) : ((n‚Åª¬π : ‚Ñö) : Œ±) = (n : Œ±)‚Åª¬π :=
  map_inv‚ÇÄ (castHom Œ±) _
#align rat.cast_inv Rat.cast_inv

@[simp, norm_cast]
theorem cast_div (m n) : ((m / n : ‚Ñö) : Œ±) = m / n :=
  map_div‚ÇÄ (castHom Œ±) _ _
#align rat.cast_div Rat.cast_div

@[simp, norm_cast]
theorem cast_zpow (q : ‚Ñö) (n : ‚Ñ§) : ((q ^ n : ‚Ñö) : Œ±) = (q : Œ±) ^ n :=
  map_zpow‚ÇÄ (castHom Œ±) q n
#align rat.cast_zpow Rat.cast_zpow

@[norm_cast]
theorem cast_mk (a b : ‚Ñ§) : (a /. b : Œ±) = a / b := by
  simp only [divInt_eq_div, cast_div, cast_coe_int]
  -- üéâ no goals
#align rat.cast_mk Rat.cast_mk

@[simp, norm_cast]
theorem cast_pow (q) (k : ‚Ñï) : ((q : ‚Ñö) ^ k : Œ±) = (q : Œ±) ^ k :=
  (castHom Œ±).map_pow q k
#align rat.cast_pow Rat.cast_pow

end WithDivRing

section LinearOrderedField

variable {K : Type*} [LinearOrderedField K]

theorem cast_pos_of_pos {r : ‚Ñö} (hr : 0 < r) : (0 : K) < r := by
  rw [Rat.cast_def]
  -- ‚ä¢ 0 < ‚Üër.num / ‚Üër.den
  exact div_pos (Int.cast_pos.2 <| num_pos_iff_pos.2 hr) (Nat.cast_pos.2 r.pos)
  -- üéâ no goals
#align rat.cast_pos_of_pos Rat.cast_pos_of_pos

@[mono]
theorem cast_strictMono : StrictMono ((‚Üë) : ‚Ñö ‚Üí K) := fun m n => by
  simpa only [sub_pos, cast_sub] using @cast_pos_of_pos K _ (n - m)
  -- üéâ no goals
#align rat.cast_strict_mono Rat.cast_strictMono

@[mono]
theorem cast_mono : Monotone ((‚Üë) : ‚Ñö ‚Üí K) :=
  cast_strictMono.monotone
#align rat.cast_mono Rat.cast_mono

/-- Coercion from `‚Ñö` as an order embedding. -/
@[simps!]
def castOrderEmbedding : ‚Ñö ‚Ü™o K :=
  OrderEmbedding.ofStrictMono (‚Üë) cast_strictMono
#align rat.cast_order_embedding Rat.castOrderEmbedding
#align rat.cast_order_embedding_apply Rat.castOrderEmbedding_apply

@[simp, norm_cast]
theorem cast_le {m n : ‚Ñö} : (m : K) ‚â§ n ‚Üî m ‚â§ n :=
  castOrderEmbedding.le_iff_le
#align rat.cast_le Rat.cast_le

@[simp, norm_cast]
theorem cast_lt {m n : ‚Ñö} : (m : K) < n ‚Üî m < n :=
  cast_strictMono.lt_iff_lt
#align rat.cast_lt Rat.cast_lt

@[simp]
theorem cast_nonneg {n : ‚Ñö} : 0 ‚â§ (n : K) ‚Üî 0 ‚â§ n := by
      norm_cast
      -- üéâ no goals
#align rat.cast_nonneg Rat.cast_nonneg

@[simp]
theorem cast_nonpos {n : ‚Ñö} : (n : K) ‚â§ 0 ‚Üî n ‚â§ 0 := by
      norm_cast
      -- üéâ no goals
#align rat.cast_nonpos Rat.cast_nonpos

@[simp]
theorem cast_pos {n : ‚Ñö} : (0 : K) < n ‚Üî 0 < n := by
      norm_cast
      -- üéâ no goals
#align rat.cast_pos Rat.cast_pos

@[simp]
theorem cast_lt_zero {n : ‚Ñö} : (n : K) < 0 ‚Üî n < 0 := by
      norm_cast
      -- üéâ no goals
#align rat.cast_lt_zero Rat.cast_lt_zero

@[simp, norm_cast]
theorem cast_min {a b : ‚Ñö} : (‚Üë(min a b) : K) = min (a : K) (b : K) :=
  (@cast_mono K _).map_min
#align rat.cast_min Rat.cast_min

@[simp, norm_cast]
theorem cast_max {a b : ‚Ñö} : (‚Üë(max a b) : K) = max (a : K) (b : K) :=
  (@cast_mono K _).map_max
#align rat.cast_max Rat.cast_max


@[simp, norm_cast]
theorem cast_abs {q : ‚Ñö} : ((|q| : ‚Ñö) : K) = |(q : K)| := by simp [abs_eq_max_neg]
                                                             -- üéâ no goals
#align rat.cast_abs Rat.cast_abs

open Set

@[simp]
theorem preimage_cast_Icc (a b : ‚Ñö) : (‚Üë) ‚Åª¬π' Icc (a : K) b = Icc a b := by
  ext x
  -- ‚ä¢ x ‚àà Rat.cast ‚Åª¬π' Icc ‚Üëa ‚Üëb ‚Üî x ‚àà Icc a b
  simp
  -- üéâ no goals
#align rat.preimage_cast_Icc Rat.preimage_cast_Icc

@[simp]
theorem preimage_cast_Ico (a b : ‚Ñö) : (‚Üë) ‚Åª¬π' Ico (a : K) b = Ico a b := by
  ext x
  -- ‚ä¢ x ‚àà Rat.cast ‚Åª¬π' Ico ‚Üëa ‚Üëb ‚Üî x ‚àà Ico a b
  simp
  -- üéâ no goals
#align rat.preimage_cast_Ico Rat.preimage_cast_Ico

@[simp]
theorem preimage_cast_Ioc (a b : ‚Ñö) : (‚Üë) ‚Åª¬π' Ioc (a : K) b = Ioc a b := by
  ext x
  -- ‚ä¢ x ‚àà Rat.cast ‚Åª¬π' Ioc ‚Üëa ‚Üëb ‚Üî x ‚àà Ioc a b
  simp
  -- üéâ no goals
#align rat.preimage_cast_Ioc Rat.preimage_cast_Ioc

@[simp]
theorem preimage_cast_Ioo (a b : ‚Ñö) : (‚Üë) ‚Åª¬π' Ioo (a : K) b = Ioo a b := by
  ext x
  -- ‚ä¢ x ‚àà Rat.cast ‚Åª¬π' Ioo ‚Üëa ‚Üëb ‚Üî x ‚àà Ioo a b
  simp
  -- üéâ no goals
#align rat.preimage_cast_Ioo Rat.preimage_cast_Ioo

@[simp]
theorem preimage_cast_Ici (a : ‚Ñö) : (‚Üë) ‚Åª¬π' Ici (a : K) = Ici a := by
  ext x
  -- ‚ä¢ x ‚àà Rat.cast ‚Åª¬π' Ici ‚Üëa ‚Üî x ‚àà Ici a
  simp
  -- üéâ no goals
#align rat.preimage_cast_Ici Rat.preimage_cast_Ici

@[simp]
theorem preimage_cast_Iic (a : ‚Ñö) : (‚Üë) ‚Åª¬π' Iic (a : K) = Iic a := by
  ext x
  -- ‚ä¢ x ‚àà Rat.cast ‚Åª¬π' Iic ‚Üëa ‚Üî x ‚àà Iic a
  simp
  -- üéâ no goals
#align rat.preimage_cast_Iic Rat.preimage_cast_Iic

@[simp]
theorem preimage_cast_Ioi (a : ‚Ñö) : (‚Üë) ‚Åª¬π' Ioi (a : K) = Ioi a := by
  ext x
  -- ‚ä¢ x ‚àà Rat.cast ‚Åª¬π' Ioi ‚Üëa ‚Üî x ‚àà Ioi a
  simp
  -- üéâ no goals
#align rat.preimage_cast_Ioi Rat.preimage_cast_Ioi

@[simp]
theorem preimage_cast_Iio (a : ‚Ñö) : (‚Üë) ‚Åª¬π' Iio (a : K) = Iio a := by
  ext x
  -- ‚ä¢ x ‚àà Rat.cast ‚Åª¬π' Iio ‚Üëa ‚Üî x ‚àà Iio a
  simp
  -- üéâ no goals
#align rat.preimage_cast_Iio Rat.preimage_cast_Iio

end LinearOrderedField

-- Porting note: statement made more explicit
@[norm_cast]
theorem cast_id (n : ‚Ñö) : Rat.cast n = n := rfl
#align rat.cast_id Rat.cast_id

@[simp]
theorem cast_eq_id : ((‚Üë) : ‚Ñö ‚Üí ‚Ñö) = id :=
  funext fun _ => rfl
#align rat.cast_eq_id Rat.cast_eq_id

@[simp]
theorem cast_hom_rat : castHom ‚Ñö = RingHom.id ‚Ñö :=
  RingHom.ext cast_id
#align rat.cast_hom_rat Rat.cast_hom_rat

end Rat

open Rat

@[simp]
theorem map_ratCast [DivisionRing Œ±] [DivisionRing Œ≤] [RingHomClass F Œ± Œ≤] (f : F) (q : ‚Ñö) :
    f q = q := by rw [cast_def, map_div‚ÇÄ, map_intCast, map_natCast, cast_def]
                  -- üéâ no goals
#align map_rat_cast map_ratCast

@[simp]
theorem eq_ratCast {k} [DivisionRing k] [RingHomClass F ‚Ñö k] (f : F) (r : ‚Ñö) : f r = r := by
  rw [‚Üê map_ratCast f, Rat.cast_id]
  -- üéâ no goals
#align eq_rat_cast eq_ratCast

namespace MonoidWithZeroHom

variable {M‚ÇÄ : Type*} [MonoidWithZero M‚ÇÄ] [MonoidWithZeroHomClass F ‚Ñö M‚ÇÄ] {f g : F}


/-- If `f` and `g` agree on the integers then they are equal `œÜ`. -/
theorem ext_rat' (h : ‚àÄ m : ‚Ñ§, f m = g m) : f = g :=
  (FunLike.ext f g) fun r => by
    rw [‚Üê r.num_div_den, div_eq_mul_inv, map_mul, map_mul, h, ‚Üê Int.cast_ofNat,
      eq_on_inv‚ÇÄ f g]
    apply h
    -- üéâ no goals
#align monoid_with_zero_hom.ext_rat' MonoidWithZeroHom.ext_rat'

/-- If `f` and `g` agree on the integers then they are equal `œÜ`.

See note [partially-applied ext lemmas] for why `comp` is used here. -/
@[ext]
theorem ext_rat {f g : ‚Ñö ‚Üí*‚ÇÄ M‚ÇÄ}
    (h : f.comp (Int.castRingHom ‚Ñö : ‚Ñ§ ‚Üí*‚ÇÄ ‚Ñö) = g.comp (Int.castRingHom ‚Ñö)) : f = g :=
  ext_rat' <| FunLike.congr_fun h
#align monoid_with_zero_hom.ext_rat MonoidWithZeroHom.ext_rat

/-- Positive integer values of a morphism `œÜ` and its value on `-1` completely determine `œÜ`. -/
theorem ext_rat_on_pnat (same_on_neg_one : f (-1) = g (-1))
    (same_on_pnat : ‚àÄ n : ‚Ñï, 0 < n ‚Üí f n = g n) : f = g :=
  ext_rat' <|
    FunLike.congr_fun <|
      show
        (f : ‚Ñö ‚Üí*‚ÇÄ M‚ÇÄ).comp (Int.castRingHom ‚Ñö : ‚Ñ§ ‚Üí*‚ÇÄ ‚Ñö) =
          (g : ‚Ñö ‚Üí*‚ÇÄ M‚ÇÄ).comp (Int.castRingHom ‚Ñö : ‚Ñ§ ‚Üí*‚ÇÄ ‚Ñö)
        from ext_int' (by simpa) (by simpa)
                          -- üéâ no goals
                                     -- üéâ no goals
#align monoid_with_zero_hom.ext_rat_on_pnat MonoidWithZeroHom.ext_rat_on_pnat

end MonoidWithZeroHom

/-- Any two ring homomorphisms from `‚Ñö` to a semiring are equal. If the codomain is a division ring,
then this lemma follows from `eq_ratCast`. -/
theorem RingHom.ext_rat {R : Type*} [Semiring R] [RingHomClass F ‚Ñö R] (f g : F) : f = g :=
  MonoidWithZeroHom.ext_rat' <|
    RingHom.congr_fun <|
      ((f : ‚Ñö ‚Üí+* R).comp (Int.castRingHom ‚Ñö)).ext_int ((g : ‚Ñö ‚Üí+* R).comp (Int.castRingHom ‚Ñö))
#align ring_hom.ext_rat RingHom.ext_rat

instance Rat.subsingleton_ringHom {R : Type*} [Semiring R] : Subsingleton (‚Ñö ‚Üí+* R) :=
  ‚ü®RingHom.ext_rat‚ü©
#align rat.subsingleton_ring_hom Rat.subsingleton_ringHom

section SMul

namespace Rat

variable {K : Type*} [DivisionRing K]

instance (priority := 100) distribSMul : DistribSMul ‚Ñö K where
  smul := (¬∑ ‚Ä¢ ¬∑)
  smul_zero a := by rw [smul_def, mul_zero]
                    -- üéâ no goals
  smul_add a x y := by rw [smul_def, smul_def, smul_def, mul_add]
                       -- üéâ no goals
#align rat.distrib_smul Rat.distribSMul

instance isScalarTower_right : IsScalarTower ‚Ñö K K :=
  ‚ü®fun a x y => by simp only [smul_def, smul_eq_mul, mul_assoc]‚ü©
                   -- üéâ no goals
#align rat.is_scalar_tower_right Rat.isScalarTower_right

end Rat

end SMul
