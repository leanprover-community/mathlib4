/-
Copyright (c) 2019 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Kevin Kappelmann
-/
import Mathlib.Algebra.Order.Floor
import Mathlib.Algebra.EuclideanDomain.Instances
import Mathlib.Data.Rat.Cast
import Mathlib.Tactic.FieldSimp

#align_import data.rat.floor from "leanprover-community/mathlib"@"e1bccd6e40ae78370f01659715d3c948716e3b7e"

/-!
# Floor Function for Rational Numbers

## Summary

We define the `FloorRing` instance on `â„š`. Some technical lemmas relating `floor` to integer
division and modulo arithmetic are derived as well as some simple inequalities.

## Tags

rat, rationals, â„š, floor
-/


open Int

namespace Rat

variable {Î± : Type*} [LinearOrderedField Î±] [FloorRing Î±]

protected theorem floor_def' (a : â„š) : a.floor = a.num / a.den := by
  rw [Rat.floor]
  -- âŠ¢ (if a.den = 1 then a.num else a.num / â†‘a.den) = a.num / â†‘a.den
  split
  -- âŠ¢ a.num = a.num / â†‘a.den
  Â· next h => simp [h]
    -- ðŸŽ‰ no goals
  Â· next => rfl
    -- ðŸŽ‰ no goals

protected theorem le_floor {z : â„¤} : âˆ€ {r : â„š}, z â‰¤ Rat.floor r â†” (z : â„š) â‰¤ r
  | âŸ¨n, d, h, câŸ© => by
    simp [Rat.floor_def']
    -- âŠ¢ z â‰¤ n / â†‘d â†” â†‘z â‰¤ mk' n d
    rw [num_den']
    -- âŠ¢ z â‰¤ n / â†‘d â†” â†‘z â‰¤ n /. â†‘d
    have h' := Int.ofNat_lt.2 (Nat.pos_of_ne_zero h)
    -- âŠ¢ z â‰¤ n / â†‘d â†” â†‘z â‰¤ n /. â†‘d
    conv =>
      rhs
      rw [coe_int_eq_divInt, Rat.le_def zero_lt_one h', mul_one]
    exact Int.le_ediv_iff_mul_le h'
    -- ðŸŽ‰ no goals
#align rat.le_floor Rat.le_floor

instance : FloorRing â„š :=
  (FloorRing.ofFloor â„š Rat.floor) fun _ _ => Rat.le_floor.symm

protected theorem floor_def {q : â„š} : âŒŠqâŒ‹ = q.num / q.den := Rat.floor_def' q
#align rat.floor_def Rat.floor_def

theorem floor_int_div_nat_eq_div {n : â„¤} {d : â„•} : âŒŠ(â†‘n : â„š) / (â†‘d : â„š)âŒ‹ = n / (â†‘d : â„¤) := by
  rw [Rat.floor_def]
  -- âŠ¢ (â†‘n / â†‘d).num / â†‘(â†‘n / â†‘d).den = n / â†‘d
  obtain rfl | hd := @eq_zero_or_pos _ _ d
  -- âŠ¢ (â†‘n / â†‘0).num / â†‘(â†‘n / â†‘0).den = n / â†‘0
  Â· simp
    -- ðŸŽ‰ no goals
  set q := (n : â„š) / d with q_eq
  -- âŠ¢ q.num / â†‘q.den = n / â†‘d
  obtain âŸ¨c, n_eq_c_mul_num, d_eq_c_mul_denomâŸ© : âˆƒ c, n = c * q.num âˆ§ (d : â„¤) = c * q.den := by
    rw [q_eq]
    exact_mod_cast @Rat.exists_eq_mul_div_num_and_eq_mul_div_den n d (by exact_mod_cast hd.ne')
  rw [n_eq_c_mul_num, d_eq_c_mul_denom]
  -- âŠ¢ q.num / â†‘q.den = c * q.num / (c * â†‘q.den)
  refine' (Int.mul_ediv_mul_of_pos _ _ <| pos_of_mul_pos_left _ <| Int.coe_nat_nonneg q.den).symm
  -- âŠ¢ 0 < c * â†‘q.den
  rwa [â† d_eq_c_mul_denom, Int.coe_nat_pos]
  -- ðŸŽ‰ no goals
#align rat.floor_int_div_nat_eq_div Rat.floor_int_div_nat_eq_div

@[simp, norm_cast]
theorem floor_cast (x : â„š) : âŒŠ(x : Î±)âŒ‹ = âŒŠxâŒ‹ :=
  floor_eq_iff.2 (by exact_mod_cast floor_eq_iff.1 (Eq.refl âŒŠxâŒ‹))
                     -- ðŸŽ‰ no goals
#align rat.floor_cast Rat.floor_cast

@[simp, norm_cast]
theorem ceil_cast (x : â„š) : âŒˆ(x : Î±)âŒ‰ = âŒˆxâŒ‰ := by
  rw [â† neg_inj, â† floor_neg, â† floor_neg, â† Rat.cast_neg, Rat.floor_cast]
  -- ðŸŽ‰ no goals
#align rat.ceil_cast Rat.ceil_cast

@[simp, norm_cast]
theorem round_cast (x : â„š) : round (x : Î±) = round x := by
  -- Porting note: `simp` worked rather than `simp [H]` in mathlib3
  have H : ((2 : â„š) : Î±) = (2 : Î±) := Rat.cast_coe_nat 2
  -- âŠ¢ round â†‘x = round x
  have : ((x + 1 / 2 : â„š) : Î±) = x + 1 / 2 := by simp [H]
  -- âŠ¢ round â†‘x = round x
  rw [round_eq, round_eq, â† this, floor_cast]
  -- ðŸŽ‰ no goals
#align rat.round_cast Rat.round_cast

@[simp, norm_cast]
theorem cast_fract (x : â„š) : (â†‘(fract x) : Î±) = fract (x : Î±) := by
  simp only [fract, cast_sub, cast_coe_int, floor_cast]
  -- ðŸŽ‰ no goals
#align rat.cast_fract Rat.cast_fract

end Rat

theorem Int.mod_nat_eq_sub_mul_floor_rat_div {n : â„¤} {d : â„•} : n % d = n - d * âŒŠ(n : â„š) / dâŒ‹ := by
  rw [eq_sub_of_add_eq <| Int.emod_add_ediv n d, Rat.floor_int_div_nat_eq_div]
  -- ðŸŽ‰ no goals
#align int.mod_nat_eq_sub_mul_floor_rat_div Int.mod_nat_eq_sub_mul_floor_rat_div

theorem Nat.coprime_sub_mul_floor_rat_div_of_coprime {n d : â„•} (n_coprime_d : n.coprime d) :
    ((n : â„¤) - d * âŒŠ(n : â„š) / dâŒ‹).natAbs.coprime d := by
  have : (n : â„¤) % d = n - d * âŒŠ(n : â„š) / dâŒ‹ := Int.mod_nat_eq_sub_mul_floor_rat_div
  -- âŠ¢ coprime (natAbs (â†‘n - â†‘d * âŒŠâ†‘n / â†‘dâŒ‹)) d
  rw [â† this]
  -- âŠ¢ coprime (natAbs (â†‘n % â†‘d)) d
  have : d.coprime n := n_coprime_d.symm
  -- âŠ¢ coprime (natAbs (â†‘n % â†‘d)) d
  rwa [Nat.coprime, Nat.gcd_rec] at this
  -- ðŸŽ‰ no goals
#align nat.coprime_sub_mul_floor_rat_div_of_coprime Nat.coprime_sub_mul_floor_rat_div_of_coprime

namespace Rat

theorem num_lt_succ_floor_mul_den (q : â„š) : q.num < (âŒŠqâŒ‹ + 1) * q.den := by
  suffices (q.num : â„š) < (âŒŠqâŒ‹ + 1) * q.den by exact_mod_cast this
  -- âŠ¢ â†‘q.num < (â†‘âŒŠqâŒ‹ + 1) * â†‘q.den
  suffices (q.num : â„š) < (q - fract q + 1) * q.den by
    have : (âŒŠqâŒ‹ : â„š) = q - fract q := eq_sub_of_add_eq <| floor_add_fract q
    rwa [this]
  suffices (q.num : â„š) < q.num + (1 - fract q) * q.den by
    have : (q - fract q + 1) * q.den = q.num + (1 - fract q) * q.den
    calc
      (q - fract q + 1) * q.den = (q + (1 - fract q)) * q.den := by ring
      _ = q * q.den + (1 - fract q) * q.den := by rw [add_mul]
      _ = q.num + (1 - fract q) * q.den := by simp

    rwa [this]
  suffices 0 < (1 - fract q) * q.den by
    rw [â† sub_lt_iff_lt_add']
    simpa
  have : 0 < 1 - fract q := by
    have : fract q < 1 := fract_lt_one q
    have : 0 + fract q < 1 := by simp [this]
    rwa [lt_sub_iff_add_lt]
  exact mul_pos this (by exact_mod_cast q.pos)
  -- ðŸŽ‰ no goals
#align rat.num_lt_succ_floor_mul_denom Rat.num_lt_succ_floor_mul_den

theorem fract_inv_num_lt_num_of_pos {q : â„š} (q_pos : 0 < q) : (fract qâ»Â¹).num < q.num := by
  -- we know that the numerator must be positive
  have q_num_pos : 0 < q.num := Rat.num_pos_iff_pos.mpr q_pos
  -- âŠ¢ (fract qâ»Â¹).num < q.num
  -- we will work with the absolute value of the numerator, which is equal to the numerator
  have q_num_abs_eq_q_num : (q.num.natAbs : â„¤) = q.num := Int.natAbs_of_nonneg q_num_pos.le
  -- âŠ¢ (fract qâ»Â¹).num < q.num
  set q_inv := (q.den : â„š) / q.num with q_inv_def
  -- âŠ¢ (fract qâ»Â¹).num < q.num
  have q_inv_eq : qâ»Â¹ = q_inv := Rat.inv_def''
  -- âŠ¢ (fract qâ»Â¹).num < q.num
  suffices (q_inv - âŒŠq_invâŒ‹).num < q.num by rwa [q_inv_eq]
  -- âŠ¢ (q_inv - â†‘âŒŠq_invâŒ‹).num < q.num
  suffices ((q.den - q.num * âŒŠq_invâŒ‹ : â„š) / q.num).num < q.num by
    field_simp [this, ne_of_gt q_num_pos]
  suffices (q.den : â„¤) - q.num * âŒŠq_invâŒ‹ < q.num by
    -- use that `q.num` and `q.den` are coprime to show that the numerator stays unreduced
    have : ((q.den - q.num * âŒŠq_invâŒ‹ : â„š) / q.num).num = q.den - q.num * âŒŠq_invâŒ‹ := by
      suffices ((q.den : â„¤) - q.num * âŒŠq_invâŒ‹).natAbs.coprime q.num.natAbs by
        exact_mod_cast Rat.num_div_eq_of_coprime q_num_pos this
      have tmp := Nat.coprime_sub_mul_floor_rat_div_of_coprime q.reduced.symm
      simpa only [Nat.cast_natAbs, abs_of_nonneg q_num_pos.le] using tmp
    rwa [this]
  -- to show the claim, start with the following inequality
  have q_inv_num_denom_ineq : qâ»Â¹.num - âŒŠqâ»Â¹âŒ‹ * qâ»Â¹.den < qâ»Â¹.den := by
    have : qâ»Â¹.num < (âŒŠqâ»Â¹âŒ‹ + 1) * qâ»Â¹.den := Rat.num_lt_succ_floor_mul_den qâ»Â¹
    have : qâ»Â¹.num < âŒŠqâ»Â¹âŒ‹ * qâ»Â¹.den + qâ»Â¹.den := by rwa [right_distrib, one_mul] at this
    rwa [â† sub_lt_iff_lt_add'] at this
  -- use that `q.num` and `q.den` are coprime to show that q_inv is the unreduced reciprocal
  -- of `q`
  have : q_inv.num = q.den âˆ§ q_inv.den = q.num.natAbs := by
    have coprime_q_denom_q_num : q.den.coprime q.num.natAbs := q.reduced.symm
    have : Int.natAbs q.den = q.den := by simp
    rw [â† this] at coprime_q_denom_q_num
    rw [q_inv_def]
    constructor
    Â· exact_mod_cast Rat.num_div_eq_of_coprime q_num_pos coprime_q_denom_q_num
    Â· suffices (((q.den : â„š) / q.num).den : â„¤) = q.num.natAbs by exact_mod_cast this
      rw [q_num_abs_eq_q_num]
      exact_mod_cast Rat.den_div_eq_of_coprime q_num_pos coprime_q_denom_q_num
  rwa [q_inv_eq, this.left, this.right, q_num_abs_eq_q_num, mul_comm] at q_inv_num_denom_ineq
  -- ðŸŽ‰ no goals
#align rat.fract_inv_num_lt_num_of_pos Rat.fract_inv_num_lt_num_of_pos

end Rat
