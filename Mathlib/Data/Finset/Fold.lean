/-
Copyright (c) 2017 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro
-/
import Mathlib.Algebra.Order.Monoid.WithTop
import Mathlib.Data.Finset.Image
import Mathlib.Data.Multiset.Fold

#align_import data.finset.fold from "leanprover-community/mathlib"@"9003f28797c0664a49e4179487267c494477d853"

/-!
# The fold operation for a commutative associative operation over a finset.
-/


namespace Finset

open Multiset

variable {Î± Î² Î³ : Type*}

/-! ### fold -/


section Fold

variable (op : Î² â†’ Î² â†’ Î²) [hc : IsCommutative Î² op] [ha : IsAssociative Î² op]

local notation a " * " b => op a b

/-- `fold op b f s` folds the commutative associative operation `op` over the
  `f`-image of `s`, i.e. `fold (+) b f {1,2,3} = f 1 + f 2 + f 3 + b`. -/
def fold (b : Î²) (f : Î± â†’ Î²) (s : Finset Î±) : Î² :=
  (s.1.map f).fold op b
#align finset.fold Finset.fold

variable {op} {f : Î± â†’ Î²} {b : Î²} {s : Finset Î±} {a : Î±}

@[simp]
theorem fold_empty : (âˆ… : Finset Î±).fold op b f = b :=
  rfl
#align finset.fold_empty Finset.fold_empty

@[simp]
theorem fold_cons (h : a âˆ‰ s) : (cons a s h).fold op b f = f a * s.fold op b f := by
  dsimp only [fold]
  -- âŠ¢ Multiset.fold op b (Multiset.map f (cons a s h).val) = op (f a) (Multiset.fo â€¦
  rw [cons_val, Multiset.map_cons, fold_cons_left]
  -- ğŸ‰ no goals
#align finset.fold_cons Finset.fold_cons

@[simp]
theorem fold_insert [DecidableEq Î±] (h : a âˆ‰ s) :
    (insert a s).fold op b f = f a * s.fold op b f := by
  unfold fold
  -- âŠ¢ Multiset.fold op b (Multiset.map f (insert a s).val) = op (f a) (Multiset.fo â€¦
  rw [insert_val, ndinsert_of_not_mem h, Multiset.map_cons, fold_cons_left]
  -- ğŸ‰ no goals
#align finset.fold_insert Finset.fold_insert

@[simp]
theorem fold_singleton : ({a} : Finset Î±).fold op b f = f a * b :=
  rfl
#align finset.fold_singleton Finset.fold_singleton

@[simp]
theorem fold_map {g : Î³ â†ª Î±} {s : Finset Î³} : (s.map g).fold op b f = s.fold op b (f âˆ˜ g) := by
  simp only [fold, map, Multiset.map_map]
  -- ğŸ‰ no goals
#align finset.fold_map Finset.fold_map

@[simp]
theorem fold_image [DecidableEq Î±] {g : Î³ â†’ Î±} {s : Finset Î³}
    (H : âˆ€ x âˆˆ s, âˆ€ y âˆˆ s, g x = g y â†’ x = y) : (s.image g).fold op b f = s.fold op b (f âˆ˜ g) := by
  simp only [fold, image_val_of_injOn H, Multiset.map_map]
  -- ğŸ‰ no goals
#align finset.fold_image Finset.fold_image

@[congr]
theorem fold_congr {g : Î± â†’ Î²} (H : âˆ€ x âˆˆ s, f x = g x) : s.fold op b f = s.fold op b g := by
  rw [fold, fold, map_congr rfl H]
  -- ğŸ‰ no goals
#align finset.fold_congr Finset.fold_congr

theorem fold_op_distrib {f g : Î± â†’ Î²} {bâ‚ bâ‚‚ : Î²} :
    (s.fold op (bâ‚ * bâ‚‚) fun x => f x * g x) = s.fold op bâ‚ f * s.fold op bâ‚‚ g := by
  simp only [fold, fold_distrib]
  -- ğŸ‰ no goals
#align finset.fold_op_distrib Finset.fold_op_distrib

theorem fold_const [hd : Decidable (s = âˆ…)] (c : Î²) (h : op c (op b c) = op b c) :
    Finset.fold op b (fun _ => c) s = if s = âˆ… then b else op b c := by
  classical
    induction' s using Finset.induction_on with x s hx IH generalizing hd
    Â· simp
    Â· simp only [Finset.fold_insert hx, IH, if_false, Finset.insert_ne_empty]
      split_ifs
      Â· rw [hc.comm]
      Â· exact h
#align finset.fold_const Finset.fold_const

theorem fold_hom {op' : Î³ â†’ Î³ â†’ Î³} [IsCommutative Î³ op'] [IsAssociative Î³ op'] {m : Î² â†’ Î³}
    (hm : âˆ€ x y, m (op x y) = op' (m x) (m y)) :
    (s.fold op' (m b) fun x => m (f x)) = m (s.fold op b f) := by
  rw [fold, fold, â† Multiset.fold_hom op hm, Multiset.map_map]
  -- âŠ¢ Multiset.fold op' (m b) (Multiset.map (fun x => m (f x)) s.val) = Multiset.f â€¦
  simp only [Function.comp_apply]
  -- ğŸ‰ no goals
#align finset.fold_hom Finset.fold_hom

theorem fold_disjUnion {sâ‚ sâ‚‚ : Finset Î±} {bâ‚ bâ‚‚ : Î²} (h) :
    (sâ‚.disjUnion sâ‚‚ h).fold op (bâ‚ * bâ‚‚) f = sâ‚.fold op bâ‚ f * sâ‚‚.fold op bâ‚‚ f :=
  (congr_arg _ <| Multiset.map_add _ _ _).trans (Multiset.fold_add _ _ _ _ _)
#align finset.fold_disj_union Finset.fold_disjUnion

theorem fold_disjiUnion {Î¹ : Type*} {s : Finset Î¹} {t : Î¹ â†’ Finset Î±} {b : Î¹ â†’ Î²} {bâ‚€ : Î²} (h) :
    (s.disjiUnion t h).fold op (s.fold op bâ‚€ b) f = s.fold op bâ‚€ fun i => (t i).fold op (b i) f :=
  (congr_arg _ <| Multiset.map_bind _ _ _).trans (Multiset.fold_bind _ _ _ _ _)
#align finset.fold_disj_Union Finset.fold_disjiUnion

theorem fold_union_inter [DecidableEq Î±] {sâ‚ sâ‚‚ : Finset Î±} {bâ‚ bâ‚‚ : Î²} :
    ((sâ‚ âˆª sâ‚‚).fold op bâ‚ f * (sâ‚ âˆ© sâ‚‚).fold op bâ‚‚ f) = sâ‚.fold op bâ‚‚ f * sâ‚‚.fold op bâ‚ f := by
  unfold fold
  -- âŠ¢ op (Multiset.fold op bâ‚ (Multiset.map f (sâ‚ âˆª sâ‚‚).val)) (Multiset.fold op bâ‚‚ â€¦
  rw [â† fold_add op, â† Multiset.map_add, union_val, inter_val, union_add_inter, Multiset.map_add,
    hc.comm, fold_add]
#align finset.fold_union_inter Finset.fold_union_inter

@[simp]
theorem fold_insert_idem [DecidableEq Î±] [hi : IsIdempotent Î² op] :
    (insert a s).fold op b f = f a * s.fold op b f := by
  by_cases h : a âˆˆ s
  -- âŠ¢ fold op b f (insert a s) = op (f a) (fold op b f s)
  Â· rw [â† insert_erase h]
    -- âŠ¢ fold op b f (insert a (insert a (erase s a))) = op (f a) (fold op b f (inser â€¦
    simp [â† ha.assoc, hi.idempotent]
    -- ğŸ‰ no goals
  Â· apply fold_insert h
    -- ğŸ‰ no goals
#align finset.fold_insert_idem Finset.fold_insert_idem

theorem fold_image_idem [DecidableEq Î±] {g : Î³ â†’ Î±} {s : Finset Î³} [hi : IsIdempotent Î² op] :
    (image g s).fold op b f = s.fold op b (f âˆ˜ g) := by
  induction' s using Finset.cons_induction with x xs hx ih
  -- âŠ¢ fold op b f (image g âˆ…) = fold op b (f âˆ˜ g) âˆ…
  Â· rw [fold_empty, image_empty, fold_empty]
    -- ğŸ‰ no goals
  Â· haveI := Classical.decEq Î³
    -- âŠ¢ fold op b f (image g (cons x xs hx)) = fold op b (f âˆ˜ g) (cons x xs hx)
    rw [fold_cons, cons_eq_insert, image_insert, fold_insert_idem, ih]
    -- âŠ¢ op (f (g x)) (fold op b (f âˆ˜ g) xs) = op ((f âˆ˜ g) x) (fold op b (f âˆ˜ g) xs)
    simp only [Function.comp_apply]
    -- ğŸ‰ no goals
#align finset.fold_image_idem Finset.fold_image_idem

/-- A stronger version of `Finset.fold_ite`, but relies on
an explicit proof of idempotency on the seed element, rather
than relying on typeclass idempotency over the whole type. -/
theorem fold_ite' {g : Î± â†’ Î²} (hb : op b b = b) (p : Î± â†’ Prop) [DecidablePred p] :
    Finset.fold op b (fun i => ite (p i) (f i) (g i)) s =
      op (Finset.fold op b f (s.filter p)) (Finset.fold op b g (s.filter fun i => Â¬p i)) := by
  classical
    induction' s using Finset.induction_on with x s hx IH
    Â· simp [hb]
    Â· simp only [Finset.fold_insert hx]
      split_ifs with h
      Â· have : x âˆ‰ Finset.filter p s := by simp [hx]
        simp [Finset.filter_insert, h, Finset.fold_insert this, ha.assoc, IH]
      Â· have : x âˆ‰ Finset.filter (fun i => Â¬ p i) s := by simp [hx]
        simp [Finset.filter_insert, h, Finset.fold_insert this, IH, â† ha.assoc, hc.comm]
#align finset.fold_ite' Finset.fold_ite'

/-- A weaker version of `Finset.fold_ite'`,
relying on typeclass idempotency over the whole type,
instead of solely on the seed element.
However, this is easier to use because it does not generate side goals. -/
theorem fold_ite [IsIdempotent Î² op] {g : Î± â†’ Î²} (p : Î± â†’ Prop) [DecidablePred p] :
    Finset.fold op b (fun i => ite (p i) (f i) (g i)) s =
      op (Finset.fold op b f (s.filter p)) (Finset.fold op b g (s.filter fun i => Â¬p i)) :=
  fold_ite' (IsIdempotent.idempotent _) _
#align finset.fold_ite Finset.fold_ite

theorem fold_op_rel_iff_and {r : Î² â†’ Î² â†’ Prop} (hr : âˆ€ {x y z}, r x (op y z) â†” r x y âˆ§ r x z)
    {c : Î²} : r c (s.fold op b f) â†” r c b âˆ§ âˆ€ x âˆˆ s, r c (f x) := by
  classical
    induction' s using Finset.induction_on with a s ha IH
    Â· simp
    rw [Finset.fold_insert ha, hr, IH, â† and_assoc, @and_comm (r c (f a)), and_assoc]
    apply and_congr Iff.rfl
    constructor
    Â· rintro âŸ¨hâ‚, hâ‚‚âŸ©
      intro b hb
      rw [Finset.mem_insert] at hb
      rcases hb with (rfl | hb) <;> solve_by_elim
    Â· intro h
      constructor
      Â· exact h a (Finset.mem_insert_self _ _)
      Â· exact fun b hb => h b <| Finset.mem_insert_of_mem hb
#align finset.fold_op_rel_iff_and Finset.fold_op_rel_iff_and

theorem fold_op_rel_iff_or {r : Î² â†’ Î² â†’ Prop} (hr : âˆ€ {x y z}, r x (op y z) â†” r x y âˆ¨ r x z)
    {c : Î²} : r c (s.fold op b f) â†” r c b âˆ¨ âˆƒ x âˆˆ s, r c (f x) := by
  classical
    induction' s using Finset.induction_on with a s ha IH
    Â· simp
    rw [Finset.fold_insert ha, hr, IH, â† or_assoc, @or_comm (r c (f a)), or_assoc]
    apply or_congr Iff.rfl
    constructor
    Â· rintro (hâ‚ | âŸ¨x, hx, hâ‚‚âŸ©)
      Â· use a
        simp [hâ‚]
      Â· refine' âŸ¨x, by simp [hx], hâ‚‚âŸ©
    Â· rintro âŸ¨x, hx, hâŸ©
      exact (mem_insert.mp hx).imp (fun hx => by rwa [hx] at h) (fun hx => âŸ¨x, hx, hâŸ©)
#align finset.fold_op_rel_iff_or Finset.fold_op_rel_iff_or

@[simp]
theorem fold_union_empty_singleton [DecidableEq Î±] (s : Finset Î±) :
    Finset.fold (Â· âˆª Â·) âˆ… singleton s = s := by
  induction' s using Finset.induction_on with a s has ih
  -- âŠ¢ fold (fun x x_1 => x âˆª x_1) âˆ… singleton âˆ… = âˆ…
  Â· simp only [fold_empty]
    -- ğŸ‰ no goals
  Â· rw [fold_insert has, ih, insert_eq]
    -- ğŸ‰ no goals
#align finset.fold_union_empty_singleton Finset.fold_union_empty_singleton

theorem fold_sup_bot_singleton [DecidableEq Î±] (s : Finset Î±) :
    Finset.fold (Â· âŠ” Â·) âŠ¥ singleton s = s :=
  fold_union_empty_singleton s
#align finset.fold_sup_bot_singleton Finset.fold_sup_bot_singleton

section Order

variable [LinearOrder Î²] (c : Î²)

theorem le_fold_min : c â‰¤ s.fold min b f â†” c â‰¤ b âˆ§ âˆ€ x âˆˆ s, c â‰¤ f x :=
  fold_op_rel_iff_and le_min_iff
#align finset.le_fold_min Finset.le_fold_min

theorem fold_min_le : s.fold min b f â‰¤ c â†” b â‰¤ c âˆ¨ âˆƒ x âˆˆ s, f x â‰¤ c := by
  show _ â‰¥ _ â†” _
  -- âŠ¢ c â‰¥ fold min b f s â†” b â‰¤ c âˆ¨ âˆƒ x, x âˆˆ s âˆ§ f x â‰¤ c
  apply fold_op_rel_iff_or
  -- âŠ¢ âˆ€ {x y z : Î²}, x â‰¥ min y z â†” x â‰¥ y âˆ¨ x â‰¥ z
  intro x y z
  -- âŠ¢ x â‰¥ min y z â†” x â‰¥ y âˆ¨ x â‰¥ z
  show _ â‰¤ _ â†” _
  -- âŠ¢ min y z â‰¤ x â†” x â‰¥ y âˆ¨ x â‰¥ z
  exact min_le_iff
  -- ğŸ‰ no goals
#align finset.fold_min_le Finset.fold_min_le

theorem lt_fold_min : c < s.fold min b f â†” c < b âˆ§ âˆ€ x âˆˆ s, c < f x :=
  fold_op_rel_iff_and lt_min_iff
#align finset.lt_fold_min Finset.lt_fold_min

theorem fold_min_lt : s.fold min b f < c â†” b < c âˆ¨ âˆƒ x âˆˆ s, f x < c := by
  show _ > _ â†” _
  -- âŠ¢ c > fold min b f s â†” b < c âˆ¨ âˆƒ x, x âˆˆ s âˆ§ f x < c
  apply fold_op_rel_iff_or
  -- âŠ¢ âˆ€ {x y z : Î²}, x > min y z â†” x > y âˆ¨ x > z
  intro x y z
  -- âŠ¢ x > min y z â†” x > y âˆ¨ x > z
  show _ < _ â†” _
  -- âŠ¢ min y z < x â†” x > y âˆ¨ x > z
  exact min_lt_iff
  -- ğŸ‰ no goals
#align finset.fold_min_lt Finset.fold_min_lt

theorem fold_max_le : s.fold max b f â‰¤ c â†” b â‰¤ c âˆ§ âˆ€ x âˆˆ s, f x â‰¤ c := by
  show _ â‰¥ _ â†” _
  -- âŠ¢ c â‰¥ fold max b f s â†” b â‰¤ c âˆ§ âˆ€ (x : Î±), x âˆˆ s â†’ f x â‰¤ c
  apply fold_op_rel_iff_and
  -- âŠ¢ âˆ€ {x y z : Î²}, x â‰¥ max y z â†” x â‰¥ y âˆ§ x â‰¥ z
  intro x y z
  -- âŠ¢ x â‰¥ max y z â†” x â‰¥ y âˆ§ x â‰¥ z
  show _ â‰¤ _ â†” _
  -- âŠ¢ max y z â‰¤ x â†” x â‰¥ y âˆ§ x â‰¥ z
  exact max_le_iff
  -- ğŸ‰ no goals
#align finset.fold_max_le Finset.fold_max_le

theorem le_fold_max : c â‰¤ s.fold max b f â†” c â‰¤ b âˆ¨ âˆƒ x âˆˆ s, c â‰¤ f x :=
  fold_op_rel_iff_or le_max_iff
#align finset.le_fold_max Finset.le_fold_max

theorem fold_max_lt : s.fold max b f < c â†” b < c âˆ§ âˆ€ x âˆˆ s, f x < c := by
  show _ > _ â†” _
  -- âŠ¢ c > fold max b f s â†” b < c âˆ§ âˆ€ (x : Î±), x âˆˆ s â†’ f x < c
  apply fold_op_rel_iff_and
  -- âŠ¢ âˆ€ {x y z : Î²}, x > max y z â†” x > y âˆ§ x > z
  intro x y z
  -- âŠ¢ x > max y z â†” x > y âˆ§ x > z
  show _ < _ â†” _
  -- âŠ¢ max y z < x â†” x > y âˆ§ x > z
  exact max_lt_iff
  -- ğŸ‰ no goals
#align finset.fold_max_lt Finset.fold_max_lt

theorem lt_fold_max : c < s.fold max b f â†” c < b âˆ¨ âˆƒ x âˆˆ s, c < f x :=
  fold_op_rel_iff_or lt_max_iff
#align finset.lt_fold_max Finset.lt_fold_max

theorem fold_max_add [Add Î²] [CovariantClass Î² Î² (Function.swap (Â· + Â·)) (Â· â‰¤ Â·)] (n : WithBot Î²)
    (s : Finset Î±) : (s.fold max âŠ¥ fun x : Î± => â†‘(f x) + n) = s.fold max âŠ¥ ((â†‘) âˆ˜ f) + n := by
  classical
    induction' s using Finset.induction_on with a s _ ih <;> simp [*, max_add_add_right]
#align finset.fold_max_add Finset.fold_max_add

end Order

end Fold

end Finset
