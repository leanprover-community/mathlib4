/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Data.Fintype.Lattice
import Mathlib.Data.Finset.Sigma

#align_import data.finset.pi_induction from "leanprover-community/mathlib"@"f93c11933efbc3c2f0299e47b8ff83e9b539cbf6"

/-!
# Induction principles for `âˆ€ i, Finset (Î± i)`

In this file we prove a few induction principles for functions `Î  i : Î¹, Finset (Î± i)` defined on a
finite type.

* `Finset.induction_on_pi` is a generic lemma that requires only `[Finite Î¹]`, `[DecidableEq Î¹]`,
  and `[âˆ€ i, DecidableEq (Î± i)]`; this version can be seen as a direct generalization of
  `Finset.induction_on`.

* `Finset.induction_on_pi_max` and `Finset.induction_on_pi_min`: generalizations of
  `Finset.induction_on_max`; these versions require `âˆ€ i, LinearOrder (Î± i)` but assume
  `âˆ€ y âˆˆ g i, y < x` and `âˆ€ y âˆˆ g i, x < y` respectively in the induction step.

## Tags
finite set, finite type, induction, function
-/


open Function

variable {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [Finite Î¹] [DecidableEq Î¹] [âˆ€ i, DecidableEq (Î± i)]

namespace Finset

/-- General theorem for `Finset.induction_on_pi`-style induction principles. -/
theorem induction_on_pi_of_choice (r : âˆ€ i, Î± i â†’ Finset (Î± i) â†’ Prop)
    (H_ex : âˆ€ (i) (s : Finset (Î± i)) (_ : s.Nonempty), âˆƒ x âˆˆ s, r i x (s.erase x))
    {p : (âˆ€ i, Finset (Î± i)) â†’ Prop} (f : âˆ€ i, Finset (Î± i)) (h0 : p fun _ â†¦ âˆ…)
    (step :
      âˆ€ (g : âˆ€ i, Finset (Î± i)) (i : Î¹) (x : Î± i),
        r i x (g i) â†’ p g â†’ p (update g i (insert x (g i)))) :
    p f := by
  cases nonempty_fintype Î¹
  -- âŠ¢ p f
  induction' hs : univ.sigma f using Finset.strongInductionOn with s ihs generalizing f; subst s
  -- âŠ¢ p f
                                                                                         -- âŠ¢ p f
  cases' eq_empty_or_nonempty (univ.sigma f) with he hne
  -- âŠ¢ p f
  Â· convert h0 using 1
    -- âŠ¢ f = fun x => âˆ…
    simpa [funext_iff] using he
    -- ðŸŽ‰ no goals
  Â· rcases sigma_nonempty.1 hne with âŸ¨i, -, hiâŸ©
    -- âŠ¢ p f
    rcases H_ex i (f i) hi with âŸ¨x, x_mem, hrâŸ©
    -- âŠ¢ p f
    set g := update f i ((f i).erase x) with hg
    -- âŠ¢ p f
-- Porting note: this tactic does not exist yet
--  clear_value g
    have hx' : x âˆ‰ g i := by
      rw [hg, update_same]
      apply not_mem_erase
    rw [show f = update g i (insert x (g i)) by
      rw [hg, update_idem, update_same, insert_erase x_mem, update_eq_self]] at hr ihs âŠ¢
    clear hg
    -- âŠ¢ p (update g i (insert x (g i)))
    rw [update_same, erase_insert hx'] at hr
    -- âŠ¢ p (update g i (insert x (g i)))
    refine step _ _ _ hr (ihs (univ.sigma g) ?_ _ rfl)
    -- âŠ¢ Finset.sigma univ g âŠ‚ Finset.sigma univ (update g i (insert x (g i)))
    rw [ssubset_iff_of_subset (sigma_mono (Subset.refl _) _)]
    -- âŠ¢ âˆƒ x_1, x_1 âˆˆ Finset.sigma univ (update g i (insert x (g i))) âˆ§ Â¬x_1 âˆˆ Finset â€¦
    exacts [âŸ¨âŸ¨i, xâŸ©, mem_sigma.2 âŸ¨mem_univ _, by simpâŸ©, by simp [hx']âŸ©,
      (@le_update_iff _ _ _ _ g g i _).2 âŸ¨subset_insert _ _, fun _ _ â†¦ le_rflâŸ©]
#align finset.induction_on_pi_of_choice Finset.induction_on_pi_of_choice

/-- Given a predicate on functions `âˆ€ i, Finset (Î± i)` defined on a finite type, it is true on all
maps provided that it is true on `fun _ â†¦ âˆ…` and for any function `g : âˆ€ i, Finset (Î± i)`, an index
`i : Î¹`, and `x âˆ‰ g i`, `p g` implies `p (update g i (insert x (g i)))`.

See also `Finset.induction_on_pi_max` and `Finset.induction_on_pi_min` for specialized versions
that require `âˆ€ i, LinearOrder (Î± i)`.  -/
theorem induction_on_pi {p : (âˆ€ i, Finset (Î± i)) â†’ Prop} (f : âˆ€ i, Finset (Î± i)) (h0 : p fun _ â†¦ âˆ…)
    (step :
      âˆ€ (g : âˆ€ i, Finset (Î± i)) (i : Î¹) (x : Î± i) (_ : x âˆ‰ g i),
        p g â†’ p (update g i (insert x (g i)))) :
    p f :=
  induction_on_pi_of_choice (fun _ x s â†¦ x âˆ‰ s) (fun _ s âŸ¨x, hxâŸ© â†¦ âŸ¨x, hx, not_mem_erase x sâŸ©) f
    h0 step
#align finset.induction_on_pi Finset.induction_on_pi

-- Porting note: this docstring is the exact translation of the one from mathlib3 but
-- the last sentence (here and in the next lemma) does make much sense to me...
/-- Given a predicate on functions `âˆ€ i, Finset (Î± i)` defined on a finite type, it is true on all
maps provided that it is true on `fun _ â†¦ âˆ…` and for any function `g : âˆ€ i, Finset (Î± i)`, an index
`i : Î¹`, and an element`x : Î± i` that is strictly greater than all elements of `g i`, `p g` implies
`p (update g i (insert x (g i)))`.

This lemma requires `LinearOrder` instances on all `Î± i`. See also `Finset.induction_on_pi` for a
version that `x âˆ‰ g i` instead of ` does not need `âˆ€ i, LinearOrder (Î± i)`. -/
theorem induction_on_pi_max [âˆ€ i, LinearOrder (Î± i)] {p : (âˆ€ i, Finset (Î± i)) â†’ Prop}
    (f : âˆ€ i, Finset (Î± i)) (h0 : p fun _ â†¦ âˆ…)
    (step :
      âˆ€ (g : âˆ€ i, Finset (Î± i)) (i : Î¹) (x : Î± i),
        (âˆ€ y âˆˆ g i, y < x) â†’ p g â†’ p (update g i (insert x (g i)))) :
    p f :=
  induction_on_pi_of_choice (fun _ x s â†¦ âˆ€ y âˆˆ s, y < x)
    (fun _ s hs â†¦ âŸ¨s.max' hs, s.max'_mem hs, fun _ â†¦ s.lt_max'_of_mem_erase_max' _âŸ©) f h0 step
#align finset.induction_on_pi_max Finset.induction_on_pi_max

/-- Given a predicate on functions `âˆ€ i, Finset (Î± i)` defined on a finite type, it is true on all
maps provided that it is true on `fun _ â†¦ âˆ…` and for any function `g : âˆ€ i, Finset (Î± i)`, an index
`i : Î¹`, and an element`x : Î± i` that is strictly less than all elements of `g i`, `p g` implies
`p (update g i (insert x (g i)))`.

This lemma requires `LinearOrder` instances on all `Î± i`. See also `Finset.induction_on_pi` for a
version that `x âˆ‰ g i` instead of ` does not need `âˆ€ i, LinearOrder (Î± i)`. -/
theorem induction_on_pi_min [âˆ€ i, LinearOrder (Î± i)] {p : (âˆ€ i, Finset (Î± i)) â†’ Prop}
    (f : âˆ€ i, Finset (Î± i)) (h0 : p fun _ â†¦ âˆ…)
    (step :
      âˆ€ (g : âˆ€ i, Finset (Î± i)) (i : Î¹) (x : Î± i),
        (âˆ€ y âˆˆ g i, x < y) â†’ p g â†’ p (update g i (insert x (g i)))) :
    p f :=
  @induction_on_pi_max Î¹ (fun i â†¦ (Î± i)áµ’áµˆ) _ _ _ _ _ _ h0 step
#align finset.induction_on_pi_min Finset.induction_on_pi_min

end Finset
