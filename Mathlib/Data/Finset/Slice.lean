/-
Copyright (c) 2021 Bhavik Mehta, YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Alena Gusakov, YaÃ«l Dillies
-/
import Mathlib.Data.Fintype.Powerset
import Mathlib.Order.Antichain
import Mathlib.Order.Interval.Finset.Nat
import Mathlib.Algebra.BigOperators.Group.Finset.Basic

/-!
# `r`-sets and slice

This file defines the `r`-th slice of a set family and provides a way to say that a set family is
made of `r`-sets.

An `r`-set is a finset of cardinality `r` (aka of *size* `r`). The `r`-th slice of a set family is
the set family made of its `r`-sets.

## Main declarations

* `Set.Sized`: `A.Sized r` means that `A` only contains `r`-sets.
* `Finset.slice`: `A.slice r` is the set of `r`-sets in `A`.

## Notation

`A # r` is notation for `A.slice r` in locale `finset_family`.
-/


open Finset Nat

variable {Î± : Type*} {Î¹ : Sort*} {Îº : Î¹ â†’ Sort*}

namespace Set

variable {A B : Set (Finset Î±)} {s : Finset Î±} {r : â„•}

/-! ### Families of `r`-sets -/


/-- `Sized r A` means that every Finset in `A` has size `r`. -/
def Sized (r : â„•) (A : Set (Finset Î±)) : Prop := âˆ€ â¦ƒxâ¦„, x âˆˆ A â†’ #x = r

theorem Sized.mono (h : A âŠ† B) (hB : B.Sized r) : A.Sized r := fun _x hx => hB <| h hx

@[simp] lemma sized_empty : (âˆ… : Set (Finset Î±)).Sized r := by simp [Sized]
@[simp] lemma sized_singleton : ({s} : Set (Finset Î±)).Sized r â†” #s = r := by simp [Sized]

theorem sized_union : (A âˆª B).Sized r â†” A.Sized r âˆ§ B.Sized r :=
  âŸ¨fun hA => âŸ¨hA.mono subset_union_left, hA.mono subset_union_rightâŸ©, fun hA _x hx =>
    hx.elim (fun h => hA.1 h) fun h => hA.2 hâŸ©

alias âŸ¨_, sized.unionâŸ© := sized_union

--TODO: A `forall_iUnion` lemma would be handy here.
@[simp]
theorem sized_iUnion {f : Î¹ â†’ Set (Finset Î±)} : (â‹ƒ i, f i).Sized r â†” âˆ€ i, (f i).Sized r := by
  simp_rw [Set.Sized, Set.mem_iUnion, forall_exists_index]
  exact forall_swap

-- `simp` normal form is `sized_iUnion`.
theorem sized_iUnionâ‚‚ {f : âˆ€ i, Îº i â†’ Set (Finset Î±)} :
    (â‹ƒ (i) (j), f i j).Sized r â†” âˆ€ i j, (f i j).Sized r := by
  simp only [Set.sized_iUnion]

protected theorem Sized.isAntichain (hA : A.Sized r) : IsAntichain (Â· âŠ† Â·) A :=
  fun _s hs _t ht h hst => h <| Finset.eq_of_subset_of_card_le hst ((hA ht).trans (hA hs).symm).le

protected theorem Sized.subsingleton (hA : A.Sized 0) : A.Subsingleton :=
  subsingleton_of_forall_eq âˆ… fun _s hs => card_eq_zero.1 <| hA hs

theorem Sized.subsingleton' [Fintype Î±] (hA : A.Sized (Fintype.card Î±)) : A.Subsingleton :=
  subsingleton_of_forall_eq Finset.univ fun s hs => s.card_eq_iff_eq_univ.1 <| hA hs

theorem Sized.empty_mem_iff (hA : A.Sized r) : âˆ… âˆˆ A â†” A = {âˆ…} :=
  hA.isAntichain.bot_mem_iff

theorem Sized.univ_mem_iff [Fintype Î±] (hA : A.Sized r) : Finset.univ âˆˆ A â†” A = {Finset.univ} :=
  hA.isAntichain.top_mem_iff

theorem sized_powersetCard (s : Finset Î±) (r : â„•) : (powersetCard r s : Set (Finset Î±)).Sized r :=
  fun _t ht => (mem_powersetCard.1 ht).2

end Set

namespace Finset

section Sized

variable [Fintype Î±] {ğ’œ : Finset (Finset Î±)} {s : Finset Î±} {r : â„•}

theorem subset_powersetCard_univ_iff : ğ’œ âŠ† powersetCard r univ â†” (ğ’œ : Set (Finset Î±)).Sized r :=
  forall_congr' fun A => by rw [mem_powersetCard_univ, mem_coe]

alias âŸ¨_, _root_.Set.Sized.subset_powersetCard_univâŸ© := subset_powersetCard_univ_iff

theorem _root_.Set.Sized.card_le (hğ’œ : (ğ’œ : Set (Finset Î±)).Sized r) :
    #ğ’œ â‰¤ (Fintype.card Î±).choose r := by
  rw [Fintype.card, â† card_powersetCard]
  exact card_le_card (subset_powersetCard_univ_iff.mpr hğ’œ)

end Sized

/-! ### Slices -/


section Slice

variable {ğ’œ : Finset (Finset Î±)} {A Aâ‚ Aâ‚‚ : Finset Î±} {r râ‚ râ‚‚ : â„•}

/-- The `r`-th slice of a set family is the subset of its elements which have cardinality `r`. -/
def slice (ğ’œ : Finset (Finset Î±)) (r : â„•) : Finset (Finset Î±) := {A âˆˆ ğ’œ | #A = r}

@[inherit_doc]
scoped[Finset] infixl:90 " # " => Finset.slice

/-- `A` is in the `r`-th slice of `ğ’œ` iff it's in `ğ’œ` and has cardinality `r`. -/
theorem mem_slice : A âˆˆ ğ’œ # r â†” A âˆˆ ğ’œ âˆ§ #A = r :=
  mem_filter

/-- The `r`-th slice of `ğ’œ` is a subset of `ğ’œ`. -/
theorem slice_subset : ğ’œ # r âŠ† ğ’œ :=
  filter_subset _ _

/-- Everything in the `r`-th slice of `ğ’œ` has size `r`. -/
theorem sized_slice : (ğ’œ # r : Set (Finset Î±)).Sized r := fun _ => And.right âˆ˜ mem_slice.mp

theorem eq_of_mem_slice (hâ‚ : A âˆˆ ğ’œ # râ‚) (hâ‚‚ : A âˆˆ ğ’œ # râ‚‚) : râ‚ = râ‚‚ :=
  (sized_slice hâ‚).symm.trans <| sized_slice hâ‚‚

/-- Elements in distinct slices must be distinct. -/
theorem ne_of_mem_slice (hâ‚ : Aâ‚ âˆˆ ğ’œ # râ‚) (hâ‚‚ : Aâ‚‚ âˆˆ ğ’œ # râ‚‚) : râ‚ â‰  râ‚‚ â†’ Aâ‚ â‰  Aâ‚‚ :=
  mt fun h => (sized_slice hâ‚).symm.trans ((congr_arg card h).trans (sized_slice hâ‚‚))

theorem pairwiseDisjoint_slice : (Set.univ : Set â„•).PairwiseDisjoint (slice ğ’œ) := fun _ _ _ _ hmn =>
  disjoint_filter.2 fun _s _hs hm hn => hmn <| hm.symm.trans hn

variable [Fintype Î±] (ğ’œ)

@[simp]
theorem biUnion_slice [DecidableEq Î±] : (Iic <| Fintype.card Î±).biUnion ğ’œ.slice = ğ’œ :=
  Subset.antisymm (biUnion_subset.2 fun _r _ => slice_subset) fun s hs =>
    mem_biUnion.2 âŸ¨#s, mem_Iic.2 <| s.card_le_univ, mem_slice.2 <| âŸ¨hs, rflâŸ©âŸ©

@[simp]
theorem sum_card_slice : âˆ‘ r âˆˆ Iic (Fintype.card Î±), #(ğ’œ # r) = #ğ’œ := by
  letI := Classical.decEq Î±
  rw [â† card_biUnion, biUnion_slice]
  exact Finset.pairwiseDisjoint_slice.subset (Set.subset_univ _)

end Slice

end Finset
