/-
Copyright (c) 2019 mathlib community. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Wojciech Nawrocki
-/
import Std.Data.RBMap
import Mathlib.Data.Num.Basic
import Mathlib.Order.Basic
import Mathlib.Init.Data.Ordering.Basic
import Mathlib.Util.CompileInductive

#align_import data.tree from "leanprover-community/mathlib"@"ed989ff568099019c6533a4d94b27d852a5710d8"

/-!
# Binary tree

Provides binary tree storage for values of any type, with O(lg n) retrieval.
See also `Lean.Data.RBTree` for red-black trees - this version allows more operations
to be defined and is better suited for in-kernel computation.

We also specialize for `Tree Unit`, which is a binary tree without any
additional data. We provide the notation `a â–³ b` for making a `Tree Unit` with children
`a` and `b`.

## References

<https://leanprover-community.github.io/archive/stream/113488-general/topic/tactic.20question.html>
-/


/-- A binary tree with values stored in non-leaf nodes. -/
inductive Tree.{u} (Î± : Type u) : Type u
  | nil : Tree Î±
  | node : Î± â†’ Tree Î± â†’ Tree Î± â†’ Tree Î±
  deriving DecidableEq, Repr -- Porting note: Removed `has_reflect`, added `Repr`.
#align tree Tree

namespace Tree

universe u

variable {Î± : Type u}

-- Porting note: replaced with `deriving Repr` which builds a better instance anyway
#noalign tree.repr

instance : Inhabited (Tree Î±) :=
  âŸ¨nilâŸ©

open Std (RBNode)

/-- Makes a `Tree Î±` out of a red-black tree. -/
def ofRBNode : RBNode Î± â†’ Tree Î±
  | RBNode.nil => nil
  | RBNode.node _color l key r => node key (ofRBNode l) (ofRBNode r)
#align tree.of_rbnode Tree.ofRBNode

/-- Finds the index of an element in the tree assuming the tree has been
constructed according to the provided decidable order on its elements.
If it hasn't, the result will be incorrect. If it has, but the element
is not in the tree, returns none. -/
def indexOf (lt : Î± â†’ Î± â†’ Prop) [DecidableRel lt] (x : Î±) : Tree Î± â†’ Option PosNum
  | nil => none
  | node a tâ‚ tâ‚‚ =>
    match cmpUsing lt x a with
    | Ordering.lt => PosNum.bit0 <$> indexOf lt x tâ‚
    | Ordering.eq => some PosNum.one
    | Ordering.gt => PosNum.bit1 <$> indexOf lt x tâ‚‚
#align tree.index_of Tree.indexOf

/-- Retrieves an element uniquely determined by a `PosNum` from the tree,
taking the following path to get to the element:
- `bit0` - go to left child
- `bit1` - go to right child
- `PosNum.one` - retrieve from here -/
def get : PosNum â†’ Tree Î± â†’ Option Î±
  | _, nil => none
  | PosNum.one, node a _tâ‚ _tâ‚‚ => some a
  | PosNum.bit0 n, node _a tâ‚ _tâ‚‚ => tâ‚.get n
  | PosNum.bit1 n, node _a _tâ‚ tâ‚‚ => tâ‚‚.get n
#align tree.get Tree.get

/-- Retrieves an element from the tree, or the provided default value
if the index is invalid. See `Tree.get`. -/
def getOrElse (n : PosNum) (t : Tree Î±) (v : Î±) : Î± :=
  (t.get n).getD v
#align tree.get_or_else Tree.getOrElse

/-- Apply a function to each value in the tree.  This is the `map` function for the `Tree` functor.
TODO: implement `Traversable Tree`. -/
def map {Î²} (f : Î± â†’ Î²) : Tree Î± â†’ Tree Î²
  | nil => nil
  | node a l r => node (f a) (map f l) (map f r)
#align tree.map Tree.map

/-- The number of internal nodes (i.e. not including leaves) of a binary tree -/
@[simp]
def numNodes : Tree Î± â†’ â„•
  | nil => 0
  | node _ a b => a.numNodes + b.numNodes + 1
#align tree.num_nodes Tree.numNodes

/-- The number of leaves of a binary tree -/
@[simp]
def numLeaves : Tree Î± â†’ â„•
  | nil => 1
  | node _ a b => a.numLeaves + b.numLeaves
#align tree.num_leaves Tree.numLeaves

/-- The height - length of the longest path from the root - of a binary tree -/
@[simp]
def height : Tree Î± â†’ â„•
  | nil => 0
  | node _ a b => max a.height b.height + 1
#align tree.height Tree.height

theorem numLeaves_eq_numNodes_succ (x : Tree Î±) : x.numLeaves = x.numNodes + 1 := by
  induction x <;> simp [*, Nat.add_comm, Nat.add_assoc, Nat.add_left_comm]
  -- âŠ¢ numLeaves nil = numNodes nil + 1
                  -- ðŸŽ‰ no goals
                  -- ðŸŽ‰ no goals
#align tree.num_leaves_eq_num_nodes_succ Tree.numLeaves_eq_numNodes_succ

theorem numLeaves_pos (x : Tree Î±) : 0 < x.numLeaves := by
  rw [numLeaves_eq_numNodes_succ]
  -- âŠ¢ 0 < numNodes x + 1
  exact x.numNodes.zero_lt_succ
  -- ðŸŽ‰ no goals
#align tree.num_leaves_pos Tree.numLeaves_pos

theorem height_le_numNodes : âˆ€ x : Tree Î±, x.height â‰¤ x.numNodes
  | nil => le_rfl
  | node _ a b =>
    Nat.succ_le_succ
      (max_le (_root_.trans a.height_le_numNodes <| a.numNodes.le_add_right _)
        (_root_.trans b.height_le_numNodes <| b.numNodes.le_add_left _))
#align tree.height_le_num_nodes Tree.height_le_numNodes

/-- The left child of the tree, or `nil` if the tree is `nil` -/
@[simp]
def left : Tree Î± â†’ Tree Î±
  | nil => nil
  | node _ l _r => l
#align tree.left Tree.left

/-- The right child of the tree, or `nil` if the tree is `nil` -/
@[simp]
def right : Tree Î± â†’ Tree Î±
  | nil => nil
  | node _ _l r => r
#align tree.right Tree.right

-- Notation for making a node with `Unit` data
scoped infixr:65 " â–³ " => Tree.node ()

-- porting note: workaround for leanprover/lean4#2049
compile_inductive% Tree

@[elab_as_elim]
def unitRecOn {motive : Tree Unit â†’ Sort*} (t : Tree Unit) (base : motive nil)
    (ind : âˆ€ x y, motive x â†’ motive y â†’ motive (x â–³ y)) : motive t :=
    -- Porting note: Old proof was `t.recOn base fun u => u.recOn ind` but
    -- structure eta makes it unnecessary (https://github.com/leanprover/lean4/issues/777).
    t.recOn base fun _u => ind
#align tree.unit_rec_on Tree.unitRecOn

theorem left_node_right_eq_self : âˆ€ {x : Tree Unit} (_hx : x â‰  nil), x.left â–³ x.right = x
  | nil, h => by trivial
                 -- ðŸŽ‰ no goals
  | node a l r, _ => rfl  -- Porting note: `a â–³ b` no longer works in pattern matching
#align tree.left_node_right_eq_self Tree.left_node_right_eq_self

end Tree
