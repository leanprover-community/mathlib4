/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Johan Commelin, Mario Carneiro
-/
import Mathlib.Data.MvPolynomial.Rename
import Mathlib.Data.Polynomial.AlgebraMap
import Mathlib.Data.MvPolynomial.Variables
import Mathlib.Data.Finsupp.Fin
import Mathlib.Logic.Equiv.Fin
import Mathlib.Algebra.BigOperators.Fin

#align_import data.mv_polynomial.equiv from "leanprover-community/mathlib"@"2f5b500a507264de86d666a5f87ddb976e2d8de4"

/-!
# Equivalences between polynomial rings

This file establishes a number of equivalences between polynomial rings,
based on equivalences between the underlying types.

## Notation

As in other polynomial files, we typically use the notation:

+ `Ïƒ : Type*` (indexing the variables)

+ `R : Type*` `[CommSemiring R]` (the coefficients)

+ `s : Ïƒ â†’â‚€ â„•`, a function from `Ïƒ` to `â„•` which is zero away from a finite set.
This will give rise to a monomial in `MvPolynomial Ïƒ R` which mathematicians might call `X^s`

+ `a : R`

+ `i : Ïƒ`, with corresponding monomial `X i`, often denoted `X_i` by mathematicians

+ `p : MvPolynomial Ïƒ R`

## Tags

equivalence, isomorphism, morphism, ring hom, hom

-/


noncomputable section

open BigOperators Polynomial Set Function Finsupp AddMonoidAlgebra

universe u v w x

variable {R : Type u} {Sâ‚ : Type v} {Sâ‚‚ : Type w} {Sâ‚ƒ : Type x}

namespace MvPolynomial

variable {Ïƒ : Type*} {a a' aâ‚ aâ‚‚ : R} {e : â„•} {s : Ïƒ â†’â‚€ â„•}

section Equiv

variable (R) [CommSemiring R]

/-- The ring isomorphism between multivariable polynomials in a single variable and
polynomials over the ground ring.
-/
@[simps]
def pUnitAlgEquiv : MvPolynomial PUnit R â‰ƒâ‚[R] R[X] where
  toFun := evalâ‚‚ Polynomial.C fun _ => Polynomial.X
  invFun := Polynomial.evalâ‚‚ MvPolynomial.C (X PUnit.unit)
  left_inv := by
    let f : R[X] â†’+* MvPolynomial PUnit R := Polynomial.evalâ‚‚RingHom MvPolynomial.C (X PUnit.unit)
    -- âŠ¢ LeftInverse (Polynomial.evalâ‚‚ C (X PUnit.unit)) (evalâ‚‚ Polynomial.C fun x => â€¦
    let g : MvPolynomial PUnit R â†’+* R[X] := evalâ‚‚Hom Polynomial.C fun _ => Polynomial.X
    -- âŠ¢ LeftInverse (Polynomial.evalâ‚‚ C (X PUnit.unit)) (evalâ‚‚ Polynomial.C fun x => â€¦
    show âˆ€ p, f.comp g p = p
    -- âŠ¢ âˆ€ (p : MvPolynomial PUnit R), â†‘(RingHom.comp f g) p = p
    apply is_id
    -- âŠ¢ RingHom.comp (RingHom.comp f g) C = C
    Â· ext a
      -- âŠ¢ coeff mâœ (â†‘(RingHom.comp (RingHom.comp f g) C) a) = coeff mâœ (â†‘C a)
      dsimp
      -- âŠ¢ coeff mâœ (Polynomial.evalâ‚‚ C (X PUnit.unit) (evalâ‚‚ Polynomial.C (fun x => Po â€¦
      rw [evalâ‚‚_C, Polynomial.evalâ‚‚_C]
      -- ğŸ‰ no goals
    Â· rintro âŸ¨âŸ©
      -- âŠ¢ â†‘(RingHom.comp f g) (X PUnit.unit) = X PUnit.unit
      dsimp
      -- âŠ¢ Polynomial.evalâ‚‚ C (X PUnit.unit) (evalâ‚‚ Polynomial.C (fun x => Polynomial.X â€¦
      rw [evalâ‚‚_X, Polynomial.evalâ‚‚_X]
      -- ğŸ‰ no goals
  right_inv p :=
    Polynomial.induction_on p (fun a => by rw [Polynomial.evalâ‚‚_C, MvPolynomial.evalâ‚‚_C])
                                           -- ğŸ‰ no goals
    (fun p q hp hq => by rw [Polynomial.evalâ‚‚_add, MvPolynomial.evalâ‚‚_add, hp, hq]) fun p n _ => by
                         -- ğŸ‰ no goals
      rw [Polynomial.evalâ‚‚_mul, Polynomial.evalâ‚‚_pow, Polynomial.evalâ‚‚_X, Polynomial.evalâ‚‚_C,
        evalâ‚‚_mul, evalâ‚‚_C, evalâ‚‚_pow, evalâ‚‚_X]
  map_mul' _ _ := evalâ‚‚_mul _ _
  map_add' _ _ := evalâ‚‚_add _ _
  commutes' _ := evalâ‚‚_C _ _ _
#align mv_polynomial.punit_alg_equiv MvPolynomial.pUnitAlgEquiv

section Map

variable {R} (Ïƒ)

/-- If `e : A â‰ƒ+* B` is an isomorphism of rings, then so is `map e`. -/
@[simps apply]
def mapEquiv [CommSemiring Sâ‚] [CommSemiring Sâ‚‚] (e : Sâ‚ â‰ƒ+* Sâ‚‚) :
    MvPolynomial Ïƒ Sâ‚ â‰ƒ+* MvPolynomial Ïƒ Sâ‚‚ :=
  { map (e : Sâ‚ â†’+* Sâ‚‚) with
    toFun := map (e : Sâ‚ â†’+* Sâ‚‚)
    invFun := map (e.symm : Sâ‚‚ â†’+* Sâ‚)
    left_inv := map_leftInverse e.left_inv
    right_inv := map_rightInverse e.right_inv }
#align mv_polynomial.map_equiv MvPolynomial.mapEquiv

@[simp]
theorem mapEquiv_refl : mapEquiv Ïƒ (RingEquiv.refl R) = RingEquiv.refl _ :=
  RingEquiv.ext map_id
#align mv_polynomial.map_equiv_refl MvPolynomial.mapEquiv_refl

@[simp]
theorem mapEquiv_symm [CommSemiring Sâ‚] [CommSemiring Sâ‚‚] (e : Sâ‚ â‰ƒ+* Sâ‚‚) :
    (mapEquiv Ïƒ e).symm = mapEquiv Ïƒ e.symm :=
  rfl
#align mv_polynomial.map_equiv_symm MvPolynomial.mapEquiv_symm

@[simp]
theorem mapEquiv_trans [CommSemiring Sâ‚] [CommSemiring Sâ‚‚] [CommSemiring Sâ‚ƒ] (e : Sâ‚ â‰ƒ+* Sâ‚‚)
    (f : Sâ‚‚ â‰ƒ+* Sâ‚ƒ) : (mapEquiv Ïƒ e).trans (mapEquiv Ïƒ f) = mapEquiv Ïƒ (e.trans f) :=
  RingEquiv.ext fun p => by
    simp only [RingEquiv.coe_trans, comp_apply, mapEquiv_apply, RingEquiv.coe_ringHom_trans,
      map_map]
#align mv_polynomial.map_equiv_trans MvPolynomial.mapEquiv_trans

variable {Aâ‚ Aâ‚‚ Aâ‚ƒ : Type*} [CommSemiring Aâ‚] [CommSemiring Aâ‚‚] [CommSemiring Aâ‚ƒ]

variable [Algebra R Aâ‚] [Algebra R Aâ‚‚] [Algebra R Aâ‚ƒ]

/-- If `e : A â‰ƒâ‚[R] B` is an isomorphism of `R`-algebras, then so is `map e`. -/
@[simps apply]
def mapAlgEquiv (e : Aâ‚ â‰ƒâ‚[R] Aâ‚‚) : MvPolynomial Ïƒ Aâ‚ â‰ƒâ‚[R] MvPolynomial Ïƒ Aâ‚‚ :=
  { mapAlgHom (e : Aâ‚ â†’â‚[R] Aâ‚‚), mapEquiv Ïƒ (e : Aâ‚ â‰ƒ+* Aâ‚‚) with toFun := map (e : Aâ‚ â†’+* Aâ‚‚) }
#align mv_polynomial.map_alg_equiv MvPolynomial.mapAlgEquiv

@[simp]
theorem mapAlgEquiv_refl : mapAlgEquiv Ïƒ (AlgEquiv.refl : Aâ‚ â‰ƒâ‚[R] Aâ‚) = AlgEquiv.refl :=
  AlgEquiv.ext map_id
#align mv_polynomial.map_alg_equiv_refl MvPolynomial.mapAlgEquiv_refl

@[simp]
theorem mapAlgEquiv_symm (e : Aâ‚ â‰ƒâ‚[R] Aâ‚‚) : (mapAlgEquiv Ïƒ e).symm = mapAlgEquiv Ïƒ e.symm :=
  rfl
#align mv_polynomial.map_alg_equiv_symm MvPolynomial.mapAlgEquiv_symm

@[simp]
theorem mapAlgEquiv_trans (e : Aâ‚ â‰ƒâ‚[R] Aâ‚‚) (f : Aâ‚‚ â‰ƒâ‚[R] Aâ‚ƒ) :
    (mapAlgEquiv Ïƒ e).trans (mapAlgEquiv Ïƒ f) = mapAlgEquiv Ïƒ (e.trans f) := by
  ext
  -- âŠ¢ coeff mâœ (â†‘(AlgEquiv.trans (mapAlgEquiv Ïƒ e) (mapAlgEquiv Ïƒ f)) aâœ) = coeff  â€¦
  simp only [AlgEquiv.trans_apply, mapAlgEquiv_apply, map_map]
  -- âŠ¢ coeff mâœ (â†‘(map (RingHom.comp â†‘f â†‘e)) aâœ) = coeff mâœ (â†‘(map â†‘(AlgEquiv.trans â€¦
  rfl
  -- ğŸ‰ no goals
#align mv_polynomial.map_alg_equiv_trans MvPolynomial.mapAlgEquiv_trans

end Map

section

variable (Sâ‚ Sâ‚‚ Sâ‚ƒ)

/-- The function from multivariable polynomials in a sum of two types,
to multivariable polynomials in one of the types,
with coefficients in multivariable polynomials in the other type.

See `sumRingEquiv` for the ring isomorphism.
-/
def sumToIter : MvPolynomial (Sum Sâ‚ Sâ‚‚) R â†’+* MvPolynomial Sâ‚ (MvPolynomial Sâ‚‚ R) :=
  evalâ‚‚Hom (C.comp C) fun bc => Sum.recOn bc X (C âˆ˜ X)
#align mv_polynomial.sum_to_iter MvPolynomial.sumToIter

@[simp]
theorem sumToIter_C (a : R) : sumToIter R Sâ‚ Sâ‚‚ (C a) = C (C a) :=
  evalâ‚‚_C _ _ a
set_option linter.uppercaseLean3 false in
#align mv_polynomial.sum_to_iter_C MvPolynomial.sumToIter_C

@[simp]
theorem sumToIter_Xl (b : Sâ‚) : sumToIter R Sâ‚ Sâ‚‚ (X (Sum.inl b)) = X b :=
  evalâ‚‚_X _ _ (Sum.inl b)
set_option linter.uppercaseLean3 false in
#align mv_polynomial.sum_to_iter_Xl MvPolynomial.sumToIter_Xl

@[simp]
theorem sumToIter_Xr (c : Sâ‚‚) : sumToIter R Sâ‚ Sâ‚‚ (X (Sum.inr c)) = C (X c) :=
  evalâ‚‚_X _ _ (Sum.inr c)
set_option linter.uppercaseLean3 false in
#align mv_polynomial.sum_to_iter_Xr MvPolynomial.sumToIter_Xr

/-- The function from multivariable polynomials in one type,
with coefficients in multivariable polynomials in another type,
to multivariable polynomials in the sum of the two types.

See `sumRingEquiv` for the ring isomorphism.
-/
def iterToSum : MvPolynomial Sâ‚ (MvPolynomial Sâ‚‚ R) â†’+* MvPolynomial (Sum Sâ‚ Sâ‚‚) R :=
  evalâ‚‚Hom (evalâ‚‚Hom C (X âˆ˜ Sum.inr)) (X âˆ˜ Sum.inl)
#align mv_polynomial.iter_to_sum MvPolynomial.iterToSum

theorem iterToSum_C_C (a : R) : iterToSum R Sâ‚ Sâ‚‚ (C (C a)) = C a :=
  Eq.trans (evalâ‚‚_C _ _ (C a)) (evalâ‚‚_C _ _ _)
set_option linter.uppercaseLean3 false in
#align mv_polynomial.iter_to_sum_C_C MvPolynomial.iterToSum_C_C

theorem iterToSum_X (b : Sâ‚) : iterToSum R Sâ‚ Sâ‚‚ (X b) = X (Sum.inl b) :=
  evalâ‚‚_X _ _ _
set_option linter.uppercaseLean3 false in
#align mv_polynomial.iter_to_sum_X MvPolynomial.iterToSum_X

theorem iterToSum_C_X (c : Sâ‚‚) : iterToSum R Sâ‚ Sâ‚‚ (C (X c)) = X (Sum.inr c) :=
  Eq.trans (evalâ‚‚_C _ _ (X c)) (evalâ‚‚_X _ _ _)
set_option linter.uppercaseLean3 false in
#align mv_polynomial.iter_to_sum_C_X MvPolynomial.iterToSum_C_X

variable (Ïƒ)

/-- The algebra isomorphism between multivariable polynomials in no variables
and the ground ring. -/
@[simps!]
def isEmptyAlgEquiv [he : IsEmpty Ïƒ] : MvPolynomial Ïƒ R â‰ƒâ‚[R] R :=
  AlgEquiv.ofAlgHom (aeval (IsEmpty.elim he)) (Algebra.ofId _ _)
    (by
      ext
      -- âŠ¢ â†‘(AlgHom.comp (aeval fun a => IsEmpty.elim he a) (Algebra.ofId R (MvPolynomi â€¦
      simp [Algebra.ofId_apply, algebraMap_eq])
      -- ğŸ‰ no goals
    (by
      ext i m
      -- âŠ¢ coeff m (â†‘(AlgHom.comp (Algebra.ofId R (MvPolynomial Ïƒ R)) (aeval fun a => I â€¦
      exact IsEmpty.elim' he i)
      -- ğŸ‰ no goals
#align mv_polynomial.is_empty_alg_equiv MvPolynomial.isEmptyAlgEquiv

/-- The ring isomorphism between multivariable polynomials in no variables
and the ground ring. -/
@[simps!]
def isEmptyRingEquiv [IsEmpty Ïƒ] : MvPolynomial Ïƒ R â‰ƒ+* R :=
  (isEmptyAlgEquiv R Ïƒ).toRingEquiv
#align mv_polynomial.is_empty_ring_equiv MvPolynomial.isEmptyRingEquiv

variable {Ïƒ}

/-- A helper function for `sumRingEquiv`. -/
@[simps]
def mvPolynomialEquivMvPolynomial [CommSemiring Sâ‚ƒ] (f : MvPolynomial Sâ‚ R â†’+* MvPolynomial Sâ‚‚ Sâ‚ƒ)
    (g : MvPolynomial Sâ‚‚ Sâ‚ƒ â†’+* MvPolynomial Sâ‚ R) (hfgC : (f.comp g).comp C = C)
    (hfgX : âˆ€ n, f (g (X n)) = X n) (hgfC : (g.comp f).comp C = C) (hgfX : âˆ€ n, g (f (X n)) = X n) :
    MvPolynomial Sâ‚ R â‰ƒ+* MvPolynomial Sâ‚‚ Sâ‚ƒ
    where
  toFun := f
  invFun := g
  left_inv := is_id (RingHom.comp _ _) hgfC hgfX
  right_inv := is_id (RingHom.comp _ _) hfgC hfgX
  map_mul' := f.map_mul
  map_add' := f.map_add
#align mv_polynomial.mv_polynomial_equiv_mv_polynomial MvPolynomial.mvPolynomialEquivMvPolynomial

/-- The ring isomorphism between multivariable polynomials in a sum of two types,
and multivariable polynomials in one of the types,
with coefficients in multivariable polynomials in the other type.
-/
def sumRingEquiv : MvPolynomial (Sum Sâ‚ Sâ‚‚) R â‰ƒ+* MvPolynomial Sâ‚ (MvPolynomial Sâ‚‚ R) := by
  apply mvPolynomialEquivMvPolynomial R (Sum Sâ‚ Sâ‚‚) _ _ (sumToIter R Sâ‚ Sâ‚‚) (iterToSum R Sâ‚ Sâ‚‚)
  Â· refine RingHom.ext (hom_eq_hom _ _ ?hC ?hX)
    -- âŠ¢ RingHom.comp (RingHom.comp (RingHom.comp (sumToIter R Sâ‚ Sâ‚‚) (iterToSum R Sâ‚ â€¦
    case hC => ext1; simp only [RingHom.comp_apply, iterToSum_C_C, sumToIter_C]
    -- âŠ¢ âˆ€ (n : Sâ‚‚), â†‘(RingHom.comp (RingHom.comp (sumToIter R Sâ‚ Sâ‚‚) (iterToSum R Sâ‚ â€¦
    -- ğŸ‰ no goals
    case hX => intro; simp only [RingHom.comp_apply, iterToSum_C_X, sumToIter_Xr]
    -- ğŸ‰ no goals
    -- ğŸ‰ no goals
  Â· simp [iterToSum_X, sumToIter_Xl]
    -- ğŸ‰ no goals
  Â· ext1; simp only [RingHom.comp_apply, sumToIter_C, iterToSum_C_C]
    -- âŠ¢ â†‘(RingHom.comp (RingHom.comp (iterToSum R Sâ‚ Sâ‚‚) (sumToIter R Sâ‚ Sâ‚‚)) C) xâœ  â€¦
          -- ğŸ‰ no goals
  Â· rintro âŸ¨âŸ© <;> simp only [sumToIter_Xl, iterToSum_X, sumToIter_Xr, iterToSum_C_X]
    -- âŠ¢ â†‘(iterToSum R Sâ‚ Sâ‚‚) (â†‘(sumToIter R Sâ‚ Sâ‚‚) (X (Sum.inl valâœ))) = X (Sum.inl  â€¦
                  -- ğŸ‰ no goals
                  -- ğŸ‰ no goals
#align mv_polynomial.sum_ring_equiv MvPolynomial.sumRingEquiv

/-- The algebra isomorphism between multivariable polynomials in a sum of two types,
and multivariable polynomials in one of the types,
with coefficients in multivariable polynomials in the other type.
-/
def sumAlgEquiv : MvPolynomial (Sum Sâ‚ Sâ‚‚) R â‰ƒâ‚[R] MvPolynomial Sâ‚ (MvPolynomial Sâ‚‚ R) :=
  { sumRingEquiv R Sâ‚ Sâ‚‚ with
    commutes' := by
      intro r
      -- âŠ¢ Equiv.toFun srcâœ.toEquiv (â†‘(algebraMap R (MvPolynomial (Sâ‚ âŠ• Sâ‚‚) R)) r) = â†‘( â€¦
      have A : algebraMap R (MvPolynomial Sâ‚ (MvPolynomial Sâ‚‚ R)) r = (C (C r) : _) := by rfl
      -- âŠ¢ Equiv.toFun srcâœ.toEquiv (â†‘(algebraMap R (MvPolynomial (Sâ‚ âŠ• Sâ‚‚) R)) r) = â†‘( â€¦
      have B : algebraMap R (MvPolynomial (Sum Sâ‚ Sâ‚‚) R) r = C r := by rfl
      -- âŠ¢ Equiv.toFun srcâœ.toEquiv (â†‘(algebraMap R (MvPolynomial (Sâ‚ âŠ• Sâ‚‚) R)) r) = â†‘( â€¦
      simp only [sumRingEquiv, mvPolynomialEquivMvPolynomial, Equiv.toFun_as_coe_apply,
        Equiv.coe_fn_mk, B, sumToIter_C, A] }
#align mv_polynomial.sum_alg_equiv MvPolynomial.sumAlgEquiv

section

-- this speeds up typeclass search in the lemma below
attribute [local instance] IsScalarTower.right

/-- The algebra isomorphism between multivariable polynomials in `Option Sâ‚` and
polynomials with coefficients in `MvPolynomial Sâ‚ R`.
-/
@[simps!]
def optionEquivLeft : MvPolynomial (Option Sâ‚) R â‰ƒâ‚[R] Polynomial (MvPolynomial Sâ‚ R) :=
  AlgEquiv.ofAlgHom (MvPolynomial.aeval fun o => o.elim Polynomial.X fun s => Polynomial.C (X s))
    (Polynomial.aevalTower (MvPolynomial.rename some) (X none))
    (by ext : 2 <;> simp [â† Polynomial.C_eq_algebraMap]) (by ext i : 2; cases i <;> simp)
        -- âŠ¢ â†‘(AlgHom.comp (AlgHom.comp (aeval fun o => Option.elim o Polynomial.X fun s  â€¦
                    -- ğŸ‰ no goals
                    -- ğŸ‰ no goals
                                                             -- âŠ¢ coeff mâœ (â†‘(AlgHom.comp (Polynomial.aevalTower (rename Option.some) (X none) â€¦
                                                                        -- âŠ¢ coeff mâœ (â†‘(AlgHom.comp (Polynomial.aevalTower (rename Option.some) (X none) â€¦
                                                                                    -- ğŸ‰ no goals
                                                                                    -- ğŸ‰ no goals
#align mv_polynomial.option_equiv_left MvPolynomial.optionEquivLeft

end

/-- The algebra isomorphism between multivariable polynomials in `Option Sâ‚` and
multivariable polynomials with coefficients in polynomials.
-/
def optionEquivRight : MvPolynomial (Option Sâ‚) R â‰ƒâ‚[R] MvPolynomial Sâ‚ R[X] :=
  AlgEquiv.ofAlgHom (MvPolynomial.aeval fun o => o.elim (C Polynomial.X) X)
    (MvPolynomial.aevalTower (Polynomial.aeval (X none)) fun i => X (Option.some i))
    (by
      ext : 2 <;>
      -- âŠ¢ â†‘(AlgHom.comp (AlgHom.comp (aeval fun o => Option.elim o (â†‘C Polynomial.X) X â€¦
        simp only [MvPolynomial.algebraMap_eq, Option.elim, AlgHom.coe_comp, AlgHom.id_comp,
          IsScalarTower.coe_toAlgHom', comp_apply, aevalTower_C, Polynomial.aeval_X, aeval_X,
          Option.elim', aevalTower_X, AlgHom.coe_id, id.def, eq_self_iff_true, imp_true_iff])
    (by
      ext âŸ¨iâŸ© : 2 <;>
      -- âŠ¢ coeff mâœ (â†‘(AlgHom.comp (aevalTower (Polynomial.aeval (X none)) fun i => X ( â€¦
        simp only [Option.elim, AlgHom.coe_comp, comp_apply, aeval_X, aevalTower_C,
          Polynomial.aeval_X, AlgHom.coe_id, id.def, aevalTower_X])
#align mv_polynomial.option_equiv_right MvPolynomial.optionEquivRight

variable (n : â„•)

/-- The algebra isomorphism between multivariable polynomials in `Fin (n + 1)` and
polynomials over multivariable polynomials in `Fin n`.
-/
def finSuccEquiv : MvPolynomial (Fin (n + 1)) R â‰ƒâ‚[R] Polynomial (MvPolynomial (Fin n) R) :=
  (renameEquiv R (_root_.finSuccEquiv n)).trans (optionEquivLeft R (Fin n))
#align mv_polynomial.fin_succ_equiv MvPolynomial.finSuccEquiv

theorem finSuccEquiv_eq :
    (finSuccEquiv R n : MvPolynomial (Fin (n + 1)) R â†’+* Polynomial (MvPolynomial (Fin n) R)) =
      evalâ‚‚Hom (Polynomial.C.comp (C : R â†’+* MvPolynomial (Fin n) R)) fun i : Fin (n + 1) =>
        Fin.cases Polynomial.X (fun k => Polynomial.C (X k)) i := by
  ext i : 2
  -- âŠ¢ â†‘(RingHom.comp (â†‘(finSuccEquiv R n)) C) i = â†‘(RingHom.comp (evalâ‚‚Hom (RingHo â€¦
  Â· simp only [finSuccEquiv, optionEquivLeft_apply, aeval_C, AlgEquiv.coe_trans, RingHom.coe_coe,
      coe_evalâ‚‚Hom, comp_apply, renameEquiv_apply, evalâ‚‚_C, RingHom.coe_comp, rename_C]
    rfl
    -- ğŸ‰ no goals
  Â· refine' Fin.cases _ _ i <;> simp [finSuccEquiv]
    -- âŠ¢ Polynomial.coeff (â†‘â†‘(finSuccEquiv R n) (X 0)) nâœ = Polynomial.coeff (â†‘(evalâ‚‚ â€¦
                                -- ğŸ‰ no goals
                                -- ğŸ‰ no goals
#align mv_polynomial.fin_succ_equiv_eq MvPolynomial.finSuccEquiv_eq

@[simp]
theorem finSuccEquiv_apply (p : MvPolynomial (Fin (n + 1)) R) :
    finSuccEquiv R n p =
      evalâ‚‚Hom (Polynomial.C.comp (C : R â†’+* MvPolynomial (Fin n) R))
        (fun i : Fin (n + 1) => Fin.cases Polynomial.X (fun k => Polynomial.C (X k)) i) p := by
  rw [â† finSuccEquiv_eq, RingHom.coe_coe]
  -- ğŸ‰ no goals
#align mv_polynomial.fin_succ_equiv_apply MvPolynomial.finSuccEquiv_apply

theorem finSuccEquiv_comp_C_eq_C {R : Type u} [CommSemiring R] (n : â„•) :
    (â†‘(MvPolynomial.finSuccEquiv R n).symm : Polynomial (MvPolynomial (Fin n) R) â†’+* _).comp
        (Polynomial.C.comp MvPolynomial.C) =
      (MvPolynomial.C : R â†’+* MvPolynomial (Fin n.succ) R) := by
  refine' RingHom.ext fun x => _
  -- âŠ¢ â†‘(RingHom.comp (â†‘(AlgEquiv.symm (finSuccEquiv R n))) (RingHom.comp Polynomia â€¦
  rw [RingHom.comp_apply]
  -- âŠ¢ â†‘â†‘(AlgEquiv.symm (finSuccEquiv R n)) (â†‘(RingHom.comp Polynomial.C C) x) = â†‘C x
  refine'
    (MvPolynomial.finSuccEquiv R n).injective
      (Trans.trans ((MvPolynomial.finSuccEquiv R n).apply_symm_apply _) _)
  simp only [MvPolynomial.finSuccEquiv_apply, MvPolynomial.evalâ‚‚Hom_C]
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align mv_polynomial.fin_succ_equiv_comp_C_eq_C MvPolynomial.finSuccEquiv_comp_C_eq_C

variable {n} {R}

theorem finSuccEquiv_X_zero : finSuccEquiv R n (X 0) = Polynomial.X := by simp
                                                                          -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align mv_polynomial.fin_succ_equiv_X_zero MvPolynomial.finSuccEquiv_X_zero

theorem finSuccEquiv_X_succ {j : Fin n} : finSuccEquiv R n (X j.succ) = Polynomial.C (X j) := by
  simp
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align mv_polynomial.fin_succ_equiv_X_succ MvPolynomial.finSuccEquiv_X_succ

/-- The coefficient of `m` in the `i`-th coefficient of `finSuccEquiv R n f` equals the
    coefficient of `Finsupp.cons i m` in `f`. -/
theorem finSuccEquiv_coeff_coeff (m : Fin n â†’â‚€ â„•) (f : MvPolynomial (Fin (n + 1)) R) (i : â„•) :
    coeff m (Polynomial.coeff (finSuccEquiv R n f) i) = coeff (m.cons i) f := by
  induction' f using MvPolynomial.induction_on' with j r p q hp hq generalizing i m
  -- âŠ¢ coeff m (Polynomial.coeff (â†‘(finSuccEquiv R n) (â†‘(monomial j) r)) i) = coeff â€¦
  swap
  -- âŠ¢ coeff m (Polynomial.coeff (â†‘(finSuccEquiv R n) (p + q)) i) = coeff (cons i m â€¦
  Â· simp only [(finSuccEquiv R n).map_add, Polynomial.coeff_add, coeff_add, hp, hq]
    -- ğŸ‰ no goals
  simp only [finSuccEquiv_apply, coe_evalâ‚‚Hom, evalâ‚‚_monomial, RingHom.coe_comp, prod_pow,
    Polynomial.coeff_C_mul, coeff_C_mul, coeff_monomial, Fin.prod_univ_succ, Fin.cases_zero,
    Fin.cases_succ, â† map_prod, â† RingHom.map_pow, Function.comp_apply]
  rw [â† mul_boole, mul_comm (Polynomial.X ^ j 0), Polynomial.coeff_C_mul_X_pow]; congr 1
  -- âŠ¢ r * coeff m (if i = â†‘j 0 then âˆ x : Fin n, X x ^ â†‘j (Fin.succ x) else 0) = r â€¦
                                                                                 -- âŠ¢ coeff m (if i = â†‘j 0 then âˆ x : Fin n, X x ^ â†‘j (Fin.succ x) else 0) = if j  â€¦
  obtain rfl | hjmi := eq_or_ne j (m.cons i)
  -- âŠ¢ coeff m (if i = â†‘(cons i m) 0 then âˆ x : Fin n, X x ^ â†‘(cons i m) (Fin.succ  â€¦
  Â· simpa only [cons_zero, cons_succ, if_pos rfl, monomial_eq, C_1, one_mul, prod_pow] using
      coeff_monomial m m (1 : R)
  Â· simp only [hjmi, if_false]
    -- âŠ¢ coeff m (if i = â†‘j 0 then âˆ x : Fin n, X x ^ â†‘j (Fin.succ x) else 0) = 0
    obtain hij | rfl := ne_or_eq i (j 0)
    -- âŠ¢ coeff m (if i = â†‘j 0 then âˆ x : Fin n, X x ^ â†‘j (Fin.succ x) else 0) = 0
    Â· simp only [hij, if_false, coeff_zero]
      -- ğŸ‰ no goals
    simp only [eq_self_iff_true, if_true]
    -- âŠ¢ coeff m (âˆ x : Fin n, X x ^ â†‘j (Fin.succ x)) = 0
    have hmj : m â‰  j.tail := by
      rintro rfl
      rw [cons_tail] at hjmi
      contradiction
    simpa only [monomial_eq, C_1, one_mul, prod_pow, Finsupp.tail_apply, if_neg hmj.symm] using
      coeff_monomial m j.tail (1 : R)
#align mv_polynomial.fin_succ_equiv_coeff_coeff MvPolynomial.finSuccEquiv_coeff_coeff

theorem eval_eq_eval_mv_eval' (s : Fin n â†’ R) (y : R) (f : MvPolynomial (Fin (n + 1)) R) :
    eval (Fin.cons y s : Fin (n + 1) â†’ R) f =
      Polynomial.eval y (Polynomial.map (eval s) (finSuccEquiv R n f)) := by
  -- turn this into a def `Polynomial.mapAlgHom`
  let Ï† : (MvPolynomial (Fin n) R)[X] â†’â‚[R] R[X] :=
    { Polynomial.mapRingHom (eval s) with
      commutes' := fun r => by
        convert Polynomial.map_C (eval s)
        exact (eval_C _).symm }
  show
    aeval (Fin.cons y s : Fin (n + 1) â†’ R) f =
      (Polynomial.aeval y).comp (Ï†.comp (finSuccEquiv R n).toAlgHom) f
  congr 2
  -- âŠ¢ aeval (Fin.cons y s) = AlgHom.comp (Polynomial.aeval y) (AlgHom.comp Ï† â†‘(fin â€¦
  apply MvPolynomial.algHom_ext
  -- âŠ¢ âˆ€ (i : Fin (n + 1)), â†‘(aeval (Fin.cons y s)) (X i) = â†‘(AlgHom.comp (Polynomi â€¦
  rw [Fin.forall_fin_succ]
  -- âŠ¢ â†‘(aeval (Fin.cons y s)) (X 0) = â†‘(AlgHom.comp (Polynomial.aeval y) (AlgHom.c â€¦
  simp only [aeval_X, Fin.cons_zero, AlgEquiv.toAlgHom_eq_coe, AlgHom.coe_comp,
    Polynomial.coe_aeval_eq_eval, Polynomial.map_C, AlgHom.coe_mk, RingHom.toFun_eq_coe,
    Polynomial.coe_mapRingHom, comp_apply, finSuccEquiv_apply, evalâ‚‚Hom_X',
    Fin.cases_zero, Polynomial.map_X, Polynomial.eval_X, Fin.cons_succ,
    Fin.cases_succ, eval_X, Polynomial.eval_C,
    RingHom.coe_mk, MonoidHom.coe_coe, AlgHom.coe_coe, implies_true, and_self,
    RingHom.toMonoidHom_eq_coe]
#align mv_polynomial.eval_eq_eval_mv_eval' MvPolynomial.eval_eq_eval_mv_eval'

theorem coeff_eval_eq_eval_coeff (s' : Fin n â†’ R) (f : Polynomial (MvPolynomial (Fin n) R))
    (i : â„•) : Polynomial.coeff (Polynomial.map (eval s') f) i = eval s' (Polynomial.coeff f i) := by
  simp only [Polynomial.coeff_map]
  -- ğŸ‰ no goals
#align mv_polynomial.coeff_eval_eq_eval_coeff MvPolynomial.coeff_eval_eq_eval_coeff

theorem support_coeff_finSuccEquiv {f : MvPolynomial (Fin (n + 1)) R} {i : â„•} {m : Fin n â†’â‚€ â„•} :
    m âˆˆ (Polynomial.coeff ((finSuccEquiv R n) f) i).support â†” Finsupp.cons i m âˆˆ f.support := by
  apply Iff.intro
  -- âŠ¢ m âˆˆ support (Polynomial.coeff (â†‘(finSuccEquiv R n) f) i) â†’ cons i m âˆˆ suppor â€¦
  Â· intro h
    -- âŠ¢ cons i m âˆˆ support f
    simpa [â† finSuccEquiv_coeff_coeff] using h
    -- ğŸ‰ no goals
  Â· intro h
    -- âŠ¢ m âˆˆ support (Polynomial.coeff (â†‘(finSuccEquiv R n) f) i)
    simpa [mem_support_iff, â† finSuccEquiv_coeff_coeff m f i] using h
    -- ğŸ‰ no goals
#align mv_polynomial.support_coeff_fin_succ_equiv MvPolynomial.support_coeff_finSuccEquiv

theorem finSuccEquiv_support (f : MvPolynomial (Fin (n + 1)) R) :
    (finSuccEquiv R n f).support = Finset.image (fun m : Fin (n + 1) â†’â‚€ â„• => m 0) f.support := by
  ext i
  -- âŠ¢ i âˆˆ Polynomial.support (â†‘(finSuccEquiv R n) f) â†” i âˆˆ Finset.image (fun m =>  â€¦
  rw [Polynomial.mem_support_iff, Finset.mem_image, nonzero_iff_exists]
  -- âŠ¢ (âˆƒ a, â†‘(Polynomial.coeff (â†‘(finSuccEquiv R n) f) i) a â‰  0) â†” âˆƒ a, a âˆˆ suppor â€¦
  constructor
  -- âŠ¢ (âˆƒ a, â†‘(Polynomial.coeff (â†‘(finSuccEquiv R n) f) i) a â‰  0) â†’ âˆƒ a, a âˆˆ suppor â€¦
  Â· rintro âŸ¨m, hmâŸ©
    -- âŠ¢ âˆƒ a, a âˆˆ support f âˆ§ â†‘a 0 = i
    refine' âŸ¨cons i m, _, cons_zero _ _âŸ©
    -- âŠ¢ cons i m âˆˆ support f
    rw [â† support_coeff_finSuccEquiv]
    -- âŠ¢ m âˆˆ support (Polynomial.coeff (â†‘(finSuccEquiv R n) f) i)
    simpa using hm
    -- ğŸ‰ no goals
  Â· rintro âŸ¨m, h, rflâŸ©
    -- âŠ¢ âˆƒ a, â†‘(Polynomial.coeff (â†‘(finSuccEquiv R n) f) (â†‘m 0)) a â‰  0
    refine' âŸ¨tail m, _âŸ©
    -- âŠ¢ â†‘(Polynomial.coeff (â†‘(finSuccEquiv R n) f) (â†‘m 0)) (tail m) â‰  0
    rwa [â† coeff, â† mem_support_iff, support_coeff_finSuccEquiv, cons_tail]
    -- ğŸ‰ no goals
#align mv_polynomial.fin_succ_equiv_support MvPolynomial.finSuccEquiv_support

theorem finSuccEquiv_support' {f : MvPolynomial (Fin (n + 1)) R} {i : â„•} :
    Finset.image (Finsupp.cons i) (Polynomial.coeff ((finSuccEquiv R n) f) i).support =
      f.support.filter fun m => m 0 = i := by
  ext m
  -- âŠ¢ m âˆˆ Finset.image (cons i) (support (Polynomial.coeff (â†‘(finSuccEquiv R n) f) â€¦
  rw [Finset.mem_filter, Finset.mem_image, mem_support_iff]
  -- âŠ¢ (âˆƒ a, a âˆˆ support (Polynomial.coeff (â†‘(finSuccEquiv R n) f) i) âˆ§ cons i a =  â€¦
  conv_lhs =>
    congr
    ext
    rw [mem_support_iff, finSuccEquiv_coeff_coeff, Ne.def]
  constructor
  -- âŠ¢ (âˆƒ x, Â¬coeff (cons i x) f = 0 âˆ§ cons i x = m) â†’ coeff m f â‰  0 âˆ§ â†‘m 0 = i
  Â· rintro âŸ¨m', âŸ¨h, hm'âŸ©âŸ©
    -- âŠ¢ coeff m f â‰  0 âˆ§ â†‘m 0 = i
    simp only [â† hm']
    -- âŠ¢ coeff (cons i m') f â‰  0 âˆ§ â†‘(cons i m') 0 = i
    exact âŸ¨h, by rw [cons_zero]âŸ©
    -- ğŸ‰ no goals
  Â· intro h
    -- âŠ¢ âˆƒ x, Â¬coeff (cons i x) f = 0 âˆ§ cons i x = m
    use tail m
    -- âŠ¢ Â¬coeff (cons i (tail m)) f = 0 âˆ§ cons i (tail m) = m
    rw [â† h.2, cons_tail]
    -- âŠ¢ Â¬coeff m f = 0 âˆ§ m = m
    simp [h.1]
    -- ğŸ‰ no goals
#align mv_polynomial.fin_succ_equiv_support' MvPolynomial.finSuccEquiv_support'

theorem support_finSuccEquiv_nonempty {f : MvPolynomial (Fin (n + 1)) R} (h : f â‰  0) :
    (finSuccEquiv R n f).support.Nonempty := by
  simp only [Finset.nonempty_iff_ne_empty, Ne, Polynomial.support_eq_empty]
  -- âŠ¢ Â¬â†‘(finSuccEquiv R n) f = 0
  refine fun c => h ?_
  -- âŠ¢ f = 0
  let ii := (finSuccEquiv R n).symm
  -- âŠ¢ f = 0
  calc
    f = ii (finSuccEquiv R n f) := by
      simpa only [â† AlgEquiv.invFun_eq_symm] using ((finSuccEquiv R n).left_inv f).symm
    _ = ii 0 := by rw [c]
    _ = 0 := by simp
#align mv_polynomial.support_fin_succ_equiv_nonempty MvPolynomial.support_finSuccEquiv_nonempty

theorem degree_finSuccEquiv {f : MvPolynomial (Fin (n + 1)) R} (h : f â‰  0) :
    (finSuccEquiv R n f).degree = degreeOf 0 f := by
  -- TODO: these should be lemmas
  have hâ‚€ : âˆ€ {Î± Î² : Type _} (f : Î± â†’ Î²), (fun x => x) âˆ˜ f = f := fun f => rfl
  -- âŠ¢ degree (â†‘(finSuccEquiv R n) f) = â†‘(degreeOf 0 f)
  have hâ‚ : âˆ€ {Î± Î² : Type _} (f : Î± â†’ Î²), f âˆ˜ (fun x => x) = f := fun f => rfl
  -- âŠ¢ degree (â†‘(finSuccEquiv R n) f) = â†‘(degreeOf 0 f)
  have hâ‚‚ : WithBot.some = Nat.cast := rfl
  -- âŠ¢ degree (â†‘(finSuccEquiv R n) f) = â†‘(degreeOf 0 f)

  have h' : ((finSuccEquiv R n f).support.sup fun x => x) = degreeOf 0 f := by
    rw [degreeOf_eq_sup, finSuccEquiv_support f, Finset.sup_image, hâ‚€]
  rw [Polynomial.degree, â† h', â† hâ‚‚, Finset.coe_sup_of_nonempty (support_finSuccEquiv_nonempty h),
    Finset.max_eq_sup_coe, hâ‚]
#align mv_polynomial.degree_fin_succ_equiv MvPolynomial.degree_finSuccEquiv

theorem natDegree_finSuccEquiv (f : MvPolynomial (Fin (n + 1)) R) :
    (finSuccEquiv R n f).natDegree = degreeOf 0 f := by
  by_cases c : f = 0
  -- âŠ¢ natDegree (â†‘(finSuccEquiv R n) f) = degreeOf 0 f
  Â· rw [c, (finSuccEquiv R n).map_zero, Polynomial.natDegree_zero, degreeOf_zero]
    -- ğŸ‰ no goals
  Â· rw [Polynomial.natDegree, degree_finSuccEquiv (by simpa only [Ne.def] )]
    -- âŠ¢ WithBot.unbot' 0 â†‘(degreeOf 0 f) = degreeOf 0 f
    erw [WithBot.unbot'_coe]
    -- âŠ¢ â†‘(degreeOf 0 f) = degreeOf 0 f
    simp
    -- ğŸ‰ no goals
#align mv_polynomial.nat_degree_fin_succ_equiv MvPolynomial.natDegree_finSuccEquiv

theorem degreeOf_coeff_finSuccEquiv (p : MvPolynomial (Fin (n + 1)) R) (j : Fin n) (i : â„•) :
    degreeOf j (Polynomial.coeff (finSuccEquiv R n p) i) â‰¤ degreeOf j.succ p := by
  rw [degreeOf_eq_sup, degreeOf_eq_sup, Finset.sup_le_iff]
  -- âŠ¢ âˆ€ (b : Fin n â†’â‚€ â„•), b âˆˆ support (Polynomial.coeff (â†‘(finSuccEquiv R n) p) i) â€¦
  intro m hm
  -- âŠ¢ â†‘m j â‰¤ Finset.sup (support p) fun m => â†‘m (Fin.succ j)
  rw [â† Finsupp.cons_succ j i m]
  -- âŠ¢ â†‘(cons i m) (Fin.succ j) â‰¤ Finset.sup (support p) fun m => â†‘m (Fin.succ j)
  exact Finset.le_sup
    (f := fun (g : Fin (Nat.succ n) â†’â‚€ â„•) => g (Fin.succ j))
    (support_coeff_finSuccEquiv.1 hm)
#align mv_polynomial.degree_of_coeff_fin_succ_equiv MvPolynomial.degreeOf_coeff_finSuccEquiv

end

end Equiv

end MvPolynomial
