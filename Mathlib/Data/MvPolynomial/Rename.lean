/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Johan Commelin, Mario Carneiro
-/
import Mathlib.Data.MvPolynomial.Basic

#align_import data.mv_polynomial.rename from "leanprover-community/mathlib"@"2f5b500a507264de86d666a5f87ddb976e2d8de4"

/-!
# Renaming variables of polynomials

This file establishes the `rename` operation on multivariate polynomials,
which modifies the set of variables.

## Main declarations

* `MvPolynomial.rename`
* `MvPolynomial.renameEquiv`

## Notation

As in other polynomial files, we typically use the notation:

+ `Ïƒ Ï„ Î± : Type*` (indexing the variables)

+ `R S : Type*` `[CommSemiring R]` `[CommSemiring S]` (the coefficients)

+ `s : Ïƒ â†’â‚€ â„•`, a function from `Ïƒ` to `â„•` which is zero away from a finite set.
This will give rise to a monomial in `MvPolynomial Ïƒ R` which mathematicians might call `X^s`

+ `r : R` elements of the coefficient ring

+ `i : Ïƒ`, with corresponding monomial `X i`, often denoted `X_i` by mathematicians

+ `p : MvPolynomial Ïƒ Î±`

-/


noncomputable section

open BigOperators

open Set Function Finsupp AddMonoidAlgebra

open BigOperators

variable {Ïƒ Ï„ Î± R S : Type*} [CommSemiring R] [CommSemiring S]

namespace MvPolynomial

section Rename

/-- Rename all the variables in a multivariable polynomial. -/
def rename (f : Ïƒ â†’ Ï„) : MvPolynomial Ïƒ R â†’â‚[R] MvPolynomial Ï„ R :=
  aeval (X âˆ˜ f)
#align mv_polynomial.rename MvPolynomial.rename

@[simp]
theorem rename_C (f : Ïƒ â†’ Ï„) (r : R) : rename f (C r) = C r :=
  evalâ‚‚_C _ _ _
set_option linter.uppercaseLean3 false in
#align mv_polynomial.rename_C MvPolynomial.rename_C

@[simp]
theorem rename_X (f : Ïƒ â†’ Ï„) (i : Ïƒ) : rename f (X i : MvPolynomial Ïƒ R) = X (f i) :=
  evalâ‚‚_X _ _ _
set_option linter.uppercaseLean3 false in
#align mv_polynomial.rename_X MvPolynomial.rename_X

theorem map_rename (f : R â†’+* S) (g : Ïƒ â†’ Ï„) (p : MvPolynomial Ïƒ R) :
    map f (rename g p) = rename g (map f p) := by
  apply MvPolynomial.induction_on p
    (fun a => by simp only [map_C, rename_C])
    (fun p q hp hq => by simp only [hp, hq, AlgHom.map_add, RingHom.map_add]) fun p n hp => by
    simp only [hp, rename_X, map_X, RingHom.map_mul, AlgHom.map_mul]
#align mv_polynomial.map_rename MvPolynomial.map_rename

@[simp]
theorem rename_rename (f : Ïƒ â†’ Ï„) (g : Ï„ â†’ Î±) (p : MvPolynomial Ïƒ R) :
    rename g (rename f p) = rename (g âˆ˜ f) p :=
  show rename g (evalâ‚‚ C (X âˆ˜ f) p) = _ by
    simp only [rename, aeval_eq_evalâ‚‚Hom]
    -- âŠ¢ â†‘(evalâ‚‚Hom (algebraMap R (MvPolynomial Î± R)) (X âˆ˜ g)) (evalâ‚‚ C (X âˆ˜ f) p) =  â€¦
    -- porting note: the Lean 3 proof of this was very fragile and included a nonterminal `simp`.
    -- Hopefully this is less prone to breaking
    rw [evalâ‚‚_comp_left (evalâ‚‚Hom (algebraMap R (MvPolynomial Î± R)) (X âˆ˜ g)) C (X âˆ˜ f) p]
    -- âŠ¢ evalâ‚‚ (RingHom.comp (evalâ‚‚Hom (algebraMap R (MvPolynomial Î± R)) (X âˆ˜ g)) C)  â€¦
    simp only [(Â· âˆ˜ Â·), evalâ‚‚Hom_X', coe_evalâ‚‚Hom]
    -- âŠ¢ evalâ‚‚ (RingHom.comp (evalâ‚‚Hom (algebraMap R (MvPolynomial Î± R)) fun x => X ( â€¦
    refine' evalâ‚‚Hom_congr _ rfl rfl
    -- âŠ¢ RingHom.comp (evalâ‚‚Hom (algebraMap R (MvPolynomial Î± R)) fun x => X (g x)) C â€¦
    ext1; simp only [comp_apply, RingHom.coe_comp, evalâ‚‚Hom_C]
    -- âŠ¢ â†‘(RingHom.comp (evalâ‚‚Hom (algebraMap R (MvPolynomial Î± R)) fun x => X (g x)) â€¦
          -- ğŸ‰ no goals
#align mv_polynomial.rename_rename MvPolynomial.rename_rename

@[simp]
theorem rename_id (p : MvPolynomial Ïƒ R) : rename id p = p :=
  evalâ‚‚_eta p
#align mv_polynomial.rename_id MvPolynomial.rename_id

theorem rename_monomial (f : Ïƒ â†’ Ï„) (d : Ïƒ â†’â‚€ â„•) (r : R) :
    rename f (monomial d r) = monomial (d.mapDomain f) r := by
  rw [rename, aeval_monomial, monomial_eq (s := Finsupp.mapDomain f d),
    Finsupp.prod_mapDomain_index]
  Â· rfl
    -- ğŸ‰ no goals
  Â· exact fun n => pow_zero _
    -- ğŸ‰ no goals
  Â· exact fun n iâ‚ iâ‚‚ => pow_add _ _ _
    -- ğŸ‰ no goals
#align mv_polynomial.rename_monomial MvPolynomial.rename_monomial

theorem rename_eq (f : Ïƒ â†’ Ï„) (p : MvPolynomial Ïƒ R) :
    rename f p = Finsupp.mapDomain (Finsupp.mapDomain f) p := by
  simp only [rename, aeval_def, evalâ‚‚, Finsupp.mapDomain, algebraMap_eq, comp_apply,
    X_pow_eq_monomial, â† monomial_finsupp_sum_index]
  rfl
  -- ğŸ‰ no goals
#align mv_polynomial.rename_eq MvPolynomial.rename_eq

theorem rename_injective (f : Ïƒ â†’ Ï„) (hf : Function.Injective f) :
    Function.Injective (rename f : MvPolynomial Ïƒ R â†’ MvPolynomial Ï„ R) := by
  have :
    (rename f : MvPolynomial Ïƒ R â†’ MvPolynomial Ï„ R) = Finsupp.mapDomain (Finsupp.mapDomain f) :=
    funext (rename_eq f)
  rw [this]
  -- âŠ¢ Injective (Finsupp.mapDomain (Finsupp.mapDomain f))
  exact Finsupp.mapDomain_injective (Finsupp.mapDomain_injective hf)
  -- ğŸ‰ no goals
#align mv_polynomial.rename_injective MvPolynomial.rename_injective

section

variable {f : Ïƒ â†’ Ï„} (hf : Function.Injective f)

open Classical

/-- Given a function between sets of variables `f : Ïƒ â†’ Ï„` that is injective with proof `hf`,
  `MvPolynomial.killCompl hf` is the `AlgHom` from `R[Ï„]` to `R[Ïƒ]` that is left inverse to
  `rename f : R[Ïƒ] â†’ R[Ï„]` and sends the variables in the complement of the range of `f` to `0`. -/
def killCompl : MvPolynomial Ï„ R â†’â‚[R] MvPolynomial Ïƒ R :=
  aeval fun i => if h : i âˆˆ Set.range f then X <| (Equiv.ofInjective f hf).symm âŸ¨i, hâŸ© else 0
#align mv_polynomial.kill_compl MvPolynomial.killCompl

theorem killCompl_comp_rename : (killCompl hf).comp (rename f) = AlgHom.id R _ :=
  algHom_ext fun i => by
    dsimp
    -- âŠ¢ â†‘(killCompl hf) (â†‘(rename f) (X i)) = X i
    rw [rename, killCompl, aeval_X, comp_apply, aeval_X, dif_pos, Equiv.ofInjective_symm_apply]
    -- ğŸ‰ no goals
#align mv_polynomial.kill_compl_comp_rename MvPolynomial.killCompl_comp_rename

@[simp]
theorem killCompl_rename_app (p : MvPolynomial Ïƒ R) : killCompl hf (rename f p) = p :=
  AlgHom.congr_fun (killCompl_comp_rename hf) p
#align mv_polynomial.kill_compl_rename_app MvPolynomial.killCompl_rename_app

end

section

variable (R)

/-- `MvPolynomial.rename e` is an equivalence when `e` is. -/
@[simps apply]
def renameEquiv (f : Ïƒ â‰ƒ Ï„) : MvPolynomial Ïƒ R â‰ƒâ‚[R] MvPolynomial Ï„ R :=
  { rename f with
    toFun := rename f
    invFun := rename f.symm
    left_inv := fun p => by rw [rename_rename, f.symm_comp_self, rename_id]
                            -- ğŸ‰ no goals
    right_inv := fun p => by rw [rename_rename, f.self_comp_symm, rename_id] }
                             -- ğŸ‰ no goals
#align mv_polynomial.rename_equiv MvPolynomial.renameEquiv

@[simp]
theorem renameEquiv_refl : renameEquiv R (Equiv.refl Ïƒ) = AlgEquiv.refl :=
  AlgEquiv.ext rename_id
#align mv_polynomial.rename_equiv_refl MvPolynomial.renameEquiv_refl

@[simp]
theorem renameEquiv_symm (f : Ïƒ â‰ƒ Ï„) : (renameEquiv R f).symm = renameEquiv R f.symm :=
  rfl
#align mv_polynomial.rename_equiv_symm MvPolynomial.renameEquiv_symm

@[simp]
theorem renameEquiv_trans (e : Ïƒ â‰ƒ Ï„) (f : Ï„ â‰ƒ Î±) :
    (renameEquiv R e).trans (renameEquiv R f) = renameEquiv R (e.trans f) :=
  AlgEquiv.ext (rename_rename e f)
#align mv_polynomial.rename_equiv_trans MvPolynomial.renameEquiv_trans

end

section

variable (f : R â†’+* S) (k : Ïƒ â†’ Ï„) (g : Ï„ â†’ S) (p : MvPolynomial Ïƒ R)

theorem evalâ‚‚_rename : (rename k p).evalâ‚‚ f g = p.evalâ‚‚ f (g âˆ˜ k) := by
  apply MvPolynomial.induction_on p <;>
    Â· intros
      -- âŠ¢ evalâ‚‚ f g (â†‘(rename k) (â†‘C aâœ)) = evalâ‚‚ f (g âˆ˜ k) (â†‘C aâœ)
      -- âŠ¢ evalâ‚‚ f g (â†‘(rename k) (pâœ + qâœ)) = evalâ‚‚ f (g âˆ˜ k) (pâœ + qâœ)
      -- ğŸ‰ no goals
      -- âŠ¢ evalâ‚‚ f g (â†‘(rename k) (pâœ * X nâœ)) = evalâ‚‚ f (g âˆ˜ k) (pâœ * X nâœ)
      -- ğŸ‰ no goals
      simp [*]
      -- ğŸ‰ no goals
#align mv_polynomial.evalâ‚‚_rename MvPolynomial.evalâ‚‚_rename

theorem evalâ‚‚Hom_rename : evalâ‚‚Hom f g (rename k p) = evalâ‚‚Hom f (g âˆ˜ k) p :=
  evalâ‚‚_rename _ _ _ _
#align mv_polynomial.evalâ‚‚_hom_rename MvPolynomial.evalâ‚‚Hom_rename

theorem aeval_rename [Algebra R S] : aeval g (rename k p) = aeval (g âˆ˜ k) p :=
  evalâ‚‚Hom_rename _ _ _ _
#align mv_polynomial.aeval_rename MvPolynomial.aeval_rename

theorem rename_evalâ‚‚ (g : Ï„ â†’ MvPolynomial Ïƒ R) :
    rename k (p.evalâ‚‚ C (g âˆ˜ k)) = (rename k p).evalâ‚‚ C (rename k âˆ˜ g) := by
  apply MvPolynomial.induction_on p <;>
    Â· intros
      -- âŠ¢ â†‘(rename k) (evalâ‚‚ C (g âˆ˜ k) (â†‘C aâœ)) = evalâ‚‚ C (â†‘(rename k) âˆ˜ g) (â†‘(rename  â€¦
      -- âŠ¢ â†‘(rename k) (evalâ‚‚ C (g âˆ˜ k) (pâœ + qâœ)) = evalâ‚‚ C (â†‘(rename k) âˆ˜ g) (â†‘(renam â€¦
      -- ğŸ‰ no goals
      -- âŠ¢ â†‘(rename k) (evalâ‚‚ C (g âˆ˜ k) (pâœ * X nâœ)) = evalâ‚‚ C (â†‘(rename k) âˆ˜ g) (â†‘(ren â€¦
      -- ğŸ‰ no goals
      simp [*]
      -- ğŸ‰ no goals
#align mv_polynomial.rename_evalâ‚‚ MvPolynomial.rename_evalâ‚‚

theorem rename_prod_mk_evalâ‚‚ (j : Ï„) (g : Ïƒ â†’ MvPolynomial Ïƒ R) :
    rename (Prod.mk j) (p.evalâ‚‚ C g) = p.evalâ‚‚ C fun x => rename (Prod.mk j) (g x) := by
  apply MvPolynomial.induction_on p <;>
    Â· intros
      -- âŠ¢ â†‘(rename (Prod.mk j)) (evalâ‚‚ C g (â†‘C aâœ)) = evalâ‚‚ C (fun x => â†‘(rename (Prod â€¦
      -- âŠ¢ â†‘(rename (Prod.mk j)) (evalâ‚‚ C g (pâœ + qâœ)) = evalâ‚‚ C (fun x => â†‘(rename (Pr â€¦
      -- ğŸ‰ no goals
      -- âŠ¢ â†‘(rename (Prod.mk j)) (evalâ‚‚ C g (pâœ * X nâœ)) = evalâ‚‚ C (fun x => â†‘(rename ( â€¦
      -- ğŸ‰ no goals
      simp [*]
      -- ğŸ‰ no goals
#align mv_polynomial.rename_prodmk_evalâ‚‚ MvPolynomial.rename_prod_mk_evalâ‚‚

theorem evalâ‚‚_rename_prod_mk (g : Ïƒ Ã— Ï„ â†’ S) (i : Ïƒ) (p : MvPolynomial Ï„ R) :
    (rename (Prod.mk i) p).evalâ‚‚ f g = evalâ‚‚ f (fun j => g (i, j)) p := by
  apply MvPolynomial.induction_on p <;>
    Â· intros
      -- âŠ¢ evalâ‚‚ f g (â†‘(rename (Prod.mk i)) (â†‘C aâœ)) = evalâ‚‚ f (fun j => g (i, j)) (â†‘C  â€¦
      -- âŠ¢ evalâ‚‚ f g (â†‘(rename (Prod.mk i)) (pâœ + qâœ)) = evalâ‚‚ f (fun j => g (i, j)) (p â€¦
      -- ğŸ‰ no goals
      -- âŠ¢ evalâ‚‚ f g (â†‘(rename (Prod.mk i)) (pâœ * X nâœ)) = evalâ‚‚ f (fun j => g (i, j))  â€¦
      -- ğŸ‰ no goals
      simp [*]
      -- ğŸ‰ no goals
#align mv_polynomial.evalâ‚‚_rename_prodmk MvPolynomial.evalâ‚‚_rename_prod_mk

theorem eval_rename_prod_mk (g : Ïƒ Ã— Ï„ â†’ R) (i : Ïƒ) (p : MvPolynomial Ï„ R) :
    eval g (rename (Prod.mk i) p) = eval (fun j => g (i, j)) p :=
  evalâ‚‚_rename_prod_mk (RingHom.id _) _ _ _
#align mv_polynomial.eval_rename_prodmk MvPolynomial.eval_rename_prod_mk

end

/-- Every polynomial is a polynomial in finitely many variables. -/
theorem exists_finset_rename (p : MvPolynomial Ïƒ R) :
    âˆƒ (s : Finset Ïƒ) (q : MvPolynomial { x // x âˆˆ s } R), p = rename (â†‘) q := by
  classical
  apply induction_on p
  Â· intro r
    exact âŸ¨âˆ…, C r, by rw [rename_C]âŸ©
  Â· rintro p q âŸ¨s, p, rflâŸ© âŸ¨t, q, rflâŸ©
    refine' âŸ¨s âˆª t, âŸ¨_, _âŸ©âŸ©
    Â· refine' rename (Subtype.map id _) p + rename (Subtype.map id _) q <;>
        simp (config := { contextual := true }) only [id.def, true_or_iff, or_true_iff,
          Finset.mem_union, forall_true_iff]
    Â· simp only [rename_rename, AlgHom.map_add]
      rfl
  Â· rintro p n âŸ¨s, p, rflâŸ©
    refine' âŸ¨insert n s, âŸ¨_, _âŸ©âŸ©
    Â· refine' rename (Subtype.map id _) p * X âŸ¨n, s.mem_insert_self nâŸ©
      simp (config := { contextual := true }) only [id.def, or_true_iff, Finset.mem_insert,
        forall_true_iff]
    Â· simp only [rename_rename, rename_X, Subtype.coe_mk, AlgHom.map_mul]
      rfl
#align mv_polynomial.exists_finset_rename MvPolynomial.exists_finset_rename

/-- `exists_finset_rename` for two polynomials at once: for any two polynomials `pâ‚`, `pâ‚‚` in a
  polynomial semiring `R[Ïƒ]` of possibly infinitely many variables, `exists_finset_renameâ‚‚` yields
  a finite subset `s` of `Ïƒ` such that both `pâ‚` and `pâ‚‚` are contained in the polynomial semiring
  `R[s]` of finitely many variables. -/
theorem exists_finset_renameâ‚‚ (pâ‚ pâ‚‚ : MvPolynomial Ïƒ R) :
    âˆƒ (s : Finset Ïƒ) (qâ‚ qâ‚‚ : MvPolynomial s R), pâ‚ = rename (â†‘) qâ‚ âˆ§ pâ‚‚ = rename (â†‘) qâ‚‚ := by
  obtain âŸ¨sâ‚, qâ‚, rflâŸ© := exists_finset_rename pâ‚
  -- âŠ¢ âˆƒ s qâ‚_1 qâ‚‚, â†‘(rename Subtype.val) qâ‚ = â†‘(rename Subtype.val) qâ‚_1 âˆ§ pâ‚‚ = â†‘( â€¦
  obtain âŸ¨sâ‚‚, qâ‚‚, rflâŸ© := exists_finset_rename pâ‚‚
  -- âŠ¢ âˆƒ s qâ‚_1 qâ‚‚_1, â†‘(rename Subtype.val) qâ‚ = â†‘(rename Subtype.val) qâ‚_1 âˆ§ â†‘(ren â€¦
  classical
    use sâ‚ âˆª sâ‚‚
    use rename (Set.inclusion <| sâ‚.subset_union_left sâ‚‚) qâ‚
    use rename (Set.inclusion <| sâ‚.subset_union_right sâ‚‚) qâ‚‚
    constructor -- porting note: was `<;> simp <;> rfl` but Lean couldn't infer the arguments
    Â· rw [rename_rename (Set.inclusion <| sâ‚.subset_union_left sâ‚‚)]
      rfl
    Â· rw [rename_rename (Set.inclusion <| sâ‚.subset_union_right sâ‚‚)]
      rfl
#align mv_polynomial.exists_finset_renameâ‚‚ MvPolynomial.exists_finset_renameâ‚‚

/-- Every polynomial is a polynomial in finitely many variables. -/
theorem exists_fin_rename (p : MvPolynomial Ïƒ R) :
    âˆƒ (n : â„•) (f : Fin n â†’ Ïƒ) (_hf : Injective f) (q : MvPolynomial (Fin n) R), p = rename f q := by
  obtain âŸ¨s, q, rflâŸ© := exists_finset_rename p
  -- âŠ¢ âˆƒ n f _hf q_1, â†‘(rename Subtype.val) q = â†‘(rename f) q_1
  let n := Fintype.card { x // x âˆˆ s }
  -- âŠ¢ âˆƒ n f _hf q_1, â†‘(rename Subtype.val) q = â†‘(rename f) q_1
  let e := Fintype.equivFin { x // x âˆˆ s }
  -- âŠ¢ âˆƒ n f _hf q_1, â†‘(rename Subtype.val) q = â†‘(rename f) q_1
  refine' âŸ¨n, (â†‘) âˆ˜ e.symm, Subtype.val_injective.comp e.symm.injective, rename e q, _âŸ©
  -- âŠ¢ â†‘(rename Subtype.val) q = â†‘(rename (Subtype.val âˆ˜ â†‘e.symm)) (â†‘(rename â†‘e) q)
  rw [â† rename_rename, rename_rename e]
  -- âŠ¢ â†‘(rename Subtype.val) q = â†‘(rename Subtype.val) (â†‘(rename (â†‘e.symm âˆ˜ â†‘e)) q)
  simp only [Function.comp, Equiv.symm_apply_apply, rename_rename]
  -- ğŸ‰ no goals
#align mv_polynomial.exists_fin_rename MvPolynomial.exists_fin_rename

end Rename

theorem evalâ‚‚_cast_comp (f : Ïƒ â†’ Ï„) (c : â„¤ â†’+* R) (g : Ï„ â†’ R) (p : MvPolynomial Ïƒ â„¤) :
    evalâ‚‚ c (g âˆ˜ f) p = evalâ‚‚ c g (rename f p) := by
  apply MvPolynomial.induction_on p (fun n => by simp only [evalâ‚‚_C, rename_C])
    (fun p q hp hq => by simp only [hp, hq, rename, evalâ‚‚_add, AlgHom.map_add])
    fun p n hp => by simp only [evalâ‚‚_mul, hp, evalâ‚‚_X, comp_apply, map_mul, rename_X, evalâ‚‚_mul]
#align mv_polynomial.evalâ‚‚_cast_comp MvPolynomial.evalâ‚‚_cast_comp

section Coeff

@[simp]
theorem coeff_rename_mapDomain (f : Ïƒ â†’ Ï„) (hf : Injective f) (Ï† : MvPolynomial Ïƒ R) (d : Ïƒ â†’â‚€ â„•) :
    (rename f Ï†).coeff (d.mapDomain f) = Ï†.coeff d := by
  classical
  apply Ï†.induction_on' (P := fun Ïˆ => coeff (Finsupp.mapDomain f d) ((rename f) Ïˆ) = coeff d Ïˆ)
  -- Lean could no longer infer the motive
  Â· intro u r
    rw [rename_monomial, coeff_monomial, coeff_monomial]
    simp only [(Finsupp.mapDomain_injective hf).eq_iff]
  Â· intros
    simp only [*, AlgHom.map_add, coeff_add]
#align mv_polynomial.coeff_rename_map_domain MvPolynomial.coeff_rename_mapDomain

theorem coeff_rename_eq_zero (f : Ïƒ â†’ Ï„) (Ï† : MvPolynomial Ïƒ R) (d : Ï„ â†’â‚€ â„•)
    (h : âˆ€ u : Ïƒ â†’â‚€ â„•, u.mapDomain f = d â†’ Ï†.coeff u = 0) : (rename f Ï†).coeff d = 0 := by
  classical
  rw [rename_eq, â† not_mem_support_iff]
  intro H
  replace H := mapDomain_support H
  rw [Finset.mem_image] at H
  obtain âŸ¨u, hu, rflâŸ© := H
  specialize h u rfl
  simp at h hu
  contradiction
#align mv_polynomial.coeff_rename_eq_zero MvPolynomial.coeff_rename_eq_zero

theorem coeff_rename_ne_zero (f : Ïƒ â†’ Ï„) (Ï† : MvPolynomial Ïƒ R) (d : Ï„ â†’â‚€ â„•)
    (h : (rename f Ï†).coeff d â‰  0) : âˆƒ u : Ïƒ â†’â‚€ â„•, u.mapDomain f = d âˆ§ Ï†.coeff u â‰  0 := by
  contrapose! h
  -- âŠ¢ coeff d (â†‘(rename f) Ï†) = 0
  apply coeff_rename_eq_zero _ _ _ h
  -- ğŸ‰ no goals
#align mv_polynomial.coeff_rename_ne_zero MvPolynomial.coeff_rename_ne_zero

@[simp]
theorem constantCoeff_rename {Ï„ : Type*} (f : Ïƒ â†’ Ï„) (Ï† : MvPolynomial Ïƒ R) :
    constantCoeff (rename f Ï†) = constantCoeff Ï† := by
  apply Ï†.induction_on
  Â· intro a
    -- âŠ¢ â†‘constantCoeff (â†‘(rename f) (â†‘C a)) = â†‘constantCoeff (â†‘C a)
    simp only [constantCoeff_C, rename_C]
    -- ğŸ‰ no goals
  Â· intro p q hp hq
    -- âŠ¢ â†‘constantCoeff (â†‘(rename f) (p + q)) = â†‘constantCoeff (p + q)
    simp only [hp, hq, RingHom.map_add, AlgHom.map_add]
    -- ğŸ‰ no goals
  Â· intro p n hp
    -- âŠ¢ â†‘constantCoeff (â†‘(rename f) (p * X n)) = â†‘constantCoeff (p * X n)
    simp only [hp, rename_X, constantCoeff_X, RingHom.map_mul, AlgHom.map_mul]
    -- ğŸ‰ no goals
#align mv_polynomial.constant_coeff_rename MvPolynomial.constantCoeff_rename

end Coeff

section Support

theorem support_rename_of_injective {p : MvPolynomial Ïƒ R} {f : Ïƒ â†’ Ï„} [DecidableEq Ï„]
    (h : Function.Injective f) :
    (rename f p).support = Finset.image (Finsupp.mapDomain f) p.support := by
  rw [rename_eq]
  -- âŠ¢ support (Finsupp.mapDomain (Finsupp.mapDomain f) p) = Finset.image (Finsupp. â€¦
  exact Finsupp.mapDomain_support_of_injective (mapDomain_injective h) _
  -- ğŸ‰ no goals
#align mv_polynomial.support_rename_of_injective MvPolynomial.support_rename_of_injective

end Support

end MvPolynomial
