/-
Copyright (c) 2020 Riccardo Brasca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Riccardo Brasca
-/
import Mathlib.Data.Polynomial.AlgebraMap
import Mathlib.Data.Polynomial.Monic

#align_import data.polynomial.lifts from "leanprover-community/mathlib"@"63417e01fbc711beaf25fa73b6edb395c0cfddd0"

/-!
# Polynomials that lift

Given semirings `R` and `S` with a morphism `f : R â†’+* S`, we define a subsemiring `lifts` of
`S[X]` by the image of `RingHom.of (map f)`.
Then, we prove that a polynomial that lifts can always be lifted to a polynomial of the same degree
and that a monic polynomial that lifts can be lifted to a monic polynomial (of the same degree).

## Main definition

* `lifts (f : R â†’+* S)` : the subsemiring of polynomials that lift.

## Main results

* `lifts_and_degree_eq` : A polynomial lifts if and only if it can be lifted to a polynomial
of the same degree.
* `lifts_and_degree_eq_and_monic` : A monic polynomial lifts if and only if it can be lifted to a
monic polynomial of the same degree.
* `lifts_iff_alg` : if `R` is commutative, a polynomial lifts if and only if it is in the image of
`mapAlg`, where `mapAlg : R[X] â†’â‚[R] S[X]` is the only `R`-algebra map
that sends `X` to `X`.

## Implementation details

In general `R` and `S` are semiring, so `lifts` is a semiring. In the case of rings, see
`lifts_iff_lifts_ring`.

Since we do not assume `R` to be commutative, we cannot say in general that the set of polynomials
that lift is a subalgebra. (By `lift_iff` this is true if `R` is commutative.)

-/


open Classical BigOperators Polynomial

noncomputable section

namespace Polynomial

universe u v w

section Semiring

variable {R : Type u} [Semiring R] {S : Type v} [Semiring S] {f : R â†’+* S}

/-- We define the subsemiring of polynomials that lifts as the image of `RingHom.of (map f)`. -/
def lifts (f : R â†’+* S) : Subsemiring S[X] :=
  RingHom.rangeS (mapRingHom f)
#align polynomial.lifts Polynomial.lifts

theorem mem_lifts (p : S[X]) : p âˆˆ lifts f â†” âˆƒ q : R[X], map f q = p := by
  simp only [coe_mapRingHom, lifts, RingHom.mem_rangeS]
  -- ğŸ‰ no goals
#align polynomial.mem_lifts Polynomial.mem_lifts

theorem lifts_iff_set_range (p : S[X]) : p âˆˆ lifts f â†” p âˆˆ Set.range (map f) := by
  simp only [coe_mapRingHom, lifts, Set.mem_range, RingHom.mem_rangeS]
  -- ğŸ‰ no goals
#align polynomial.lifts_iff_set_range Polynomial.lifts_iff_set_range

theorem lifts_iff_ringHom_rangeS (p : S[X]) : p âˆˆ lifts f â†” p âˆˆ (mapRingHom f).rangeS := by
  simp only [coe_mapRingHom, lifts, Set.mem_range, RingHom.mem_rangeS]
  -- ğŸ‰ no goals
#align polynomial.lifts_iff_ring_hom_srange Polynomial.lifts_iff_ringHom_rangeS

theorem lifts_iff_coeff_lifts (p : S[X]) : p âˆˆ lifts f â†” âˆ€ n : â„•, p.coeff n âˆˆ Set.range f := by
  rw [lifts_iff_ringHom_rangeS, mem_map_rangeS f]
  -- âŠ¢ (âˆ€ (n : â„•), coeff p n âˆˆ RingHom.rangeS f) â†” âˆ€ (n : â„•), coeff p n âˆˆ Set.range â€¦
  rfl
  -- ğŸ‰ no goals
#align polynomial.lifts_iff_coeff_lifts Polynomial.lifts_iff_coeff_lifts

/-- If `(r : R)`, then `C (f r)` lifts. -/
theorem C_mem_lifts (f : R â†’+* S) (r : R) : C (f r) âˆˆ lifts f :=
  âŸ¨C r, by
    simp only [coe_mapRingHom, map_C, Set.mem_univ, Subsemiring.coe_top, eq_self_iff_true,
      and_self_iff]âŸ©
set_option linter.uppercaseLean3 false in
#align polynomial.C_mem_lifts Polynomial.C_mem_lifts

/-- If `(s : S)` is in the image of `f`, then `C s` lifts. -/
theorem C'_mem_lifts {f : R â†’+* S} {s : S} (h : s âˆˆ Set.range f) : C s âˆˆ lifts f := by
  obtain âŸ¨r, rflâŸ© := Set.mem_range.1 h
  -- âŠ¢ â†‘C (â†‘f r) âˆˆ lifts f
  use C r
  -- âŠ¢ â†‘(mapRingHom f) (â†‘C r) = â†‘C (â†‘f r)
  simp only [coe_mapRingHom, map_C, Set.mem_univ, Subsemiring.coe_top, eq_self_iff_true,
    and_self_iff]
set_option linter.uppercaseLean3 false in
#align polynomial.C'_mem_lifts Polynomial.C'_mem_lifts

/-- The polynomial `X` lifts. -/
theorem X_mem_lifts (f : R â†’+* S) : (X : S[X]) âˆˆ lifts f :=
  âŸ¨X, by
    simp only [coe_mapRingHom, Set.mem_univ, Subsemiring.coe_top, eq_self_iff_true, map_X,
      and_self_iff]âŸ©
set_option linter.uppercaseLean3 false in
#align polynomial.X_mem_lifts Polynomial.X_mem_lifts

/-- The polynomial `X ^ n` lifts. -/
theorem X_pow_mem_lifts (f : R â†’+* S) (n : â„•) : (X ^ n : S[X]) âˆˆ lifts f :=
  âŸ¨X ^ n, by
    simp only [coe_mapRingHom, map_pow, Set.mem_univ, Subsemiring.coe_top, eq_self_iff_true,
      map_X, and_self_iff]âŸ©
set_option linter.uppercaseLean3 false in
#align polynomial.X_pow_mem_lifts Polynomial.X_pow_mem_lifts

/-- If `p` lifts and `(r : R)` then `r * p` lifts. -/
theorem base_mul_mem_lifts {p : S[X]} (r : R) (hp : p âˆˆ lifts f) : C (f r) * p âˆˆ lifts f := by
  simp only [lifts, RingHom.mem_rangeS] at hp âŠ¢
  -- âŠ¢ âˆƒ x, â†‘(mapRingHom f) x = â†‘C (â†‘f r) * p
  obtain âŸ¨pâ‚, rflâŸ© := hp
  -- âŠ¢ âˆƒ x, â†‘(mapRingHom f) x = â†‘C (â†‘f r) * â†‘(mapRingHom f) pâ‚
  use C r * pâ‚
  -- âŠ¢ â†‘(mapRingHom f) (â†‘C r * pâ‚) = â†‘C (â†‘f r) * â†‘(mapRingHom f) pâ‚
  simp only [coe_mapRingHom, map_C, map_mul]
  -- ğŸ‰ no goals
#align polynomial.base_mul_mem_lifts Polynomial.base_mul_mem_lifts

/-- If `(s : S)` is in the image of `f`, then `monomial n s` lifts. -/
theorem monomial_mem_lifts {s : S} (n : â„•) (h : s âˆˆ Set.range f) : monomial n s âˆˆ lifts f := by
  obtain âŸ¨r, rflâŸ© := Set.mem_range.1 h
  -- âŠ¢ â†‘(monomial n) (â†‘f r) âˆˆ lifts f
  use monomial n r
  -- âŠ¢ â†‘(mapRingHom f) (â†‘(monomial n) r) = â†‘(monomial n) (â†‘f r)
  simp only [coe_mapRingHom, Set.mem_univ, map_monomial, Subsemiring.coe_top, eq_self_iff_true,
    and_self_iff]
#align polynomial.monomial_mem_lifts Polynomial.monomial_mem_lifts

/-- If `p` lifts then `p.erase n` lifts. -/
theorem erase_mem_lifts {p : S[X]} (n : â„•) (h : p âˆˆ lifts f) : p.erase n âˆˆ lifts f := by
  rw [lifts_iff_ringHom_rangeS, mem_map_rangeS] at h âŠ¢
  -- âŠ¢ âˆ€ (n_1 : â„•), coeff (erase n p) n_1 âˆˆ RingHom.rangeS f
  intro k
  -- âŠ¢ coeff (erase n p) k âˆˆ RingHom.rangeS f
  by_cases hk : k = n
  -- âŠ¢ coeff (erase n p) k âˆˆ RingHom.rangeS f
  Â· use 0
    -- âŠ¢ â†‘f 0 = coeff (erase n p) k
    simp only [hk, RingHom.map_zero, erase_same]
    -- ğŸ‰ no goals
  obtain âŸ¨i, hiâŸ© := h k
  -- âŠ¢ coeff (erase n p) k âˆˆ RingHom.rangeS f
  use i
  -- âŠ¢ â†‘f i = coeff (erase n p) k
  simp only [hi, hk, erase_ne, Ne.def, not_false_iff]
  -- ğŸ‰ no goals
#align polynomial.erase_mem_lifts Polynomial.erase_mem_lifts

section LiftDeg

theorem monomial_mem_lifts_and_degree_eq {s : S} {n : â„•} (hl : monomial n s âˆˆ lifts f) :
    âˆƒ q : R[X], map f q = monomial n s âˆ§ q.degree = (monomial n s).degree := by
  by_cases hzero : s = 0
  -- âŠ¢ âˆƒ q, map f q = â†‘(monomial n) s âˆ§ degree q = degree (â†‘(monomial n) s)
  Â· use 0
    -- âŠ¢ map f 0 = â†‘(monomial n) s âˆ§ degree 0 = degree (â†‘(monomial n) s)
    simp only [hzero, degree_zero, eq_self_iff_true, and_self_iff, monomial_zero_right,
      Polynomial.map_zero]
  rw [lifts_iff_set_range] at hl
  -- âŠ¢ âˆƒ q, map f q = â†‘(monomial n) s âˆ§ degree q = degree (â†‘(monomial n) s)
  obtain âŸ¨q, hqâŸ© := hl
  -- âŠ¢ âˆƒ q, map f q = â†‘(monomial n) s âˆ§ degree q = degree (â†‘(monomial n) s)
  replace hq := (ext_iff.1 hq) n
  -- âŠ¢ âˆƒ q, map f q = â†‘(monomial n) s âˆ§ degree q = degree (â†‘(monomial n) s)
  have hcoeff : f (q.coeff n) = s := by
    simp [coeff_monomial] at hq
    exact hq
  use monomial n (q.coeff n)
  -- âŠ¢ map f (â†‘(monomial n) (coeff q n)) = â†‘(monomial n) s âˆ§ degree (â†‘(monomial n)  â€¦
  constructor
  -- âŠ¢ map f (â†‘(monomial n) (coeff q n)) = â†‘(monomial n) s
  Â· simp only [hcoeff, map_monomial]
    -- ğŸ‰ no goals
  have hqzero : q.coeff n â‰  0 := by
    intro habs
    simp only [habs, RingHom.map_zero] at hcoeff
    exact hzero hcoeff.symm
  rw [â† C_mul_X_pow_eq_monomial]
  -- âŠ¢ degree (â†‘C (coeff q n) * X ^ n) = degree (â†‘(monomial n) s)
  rw [â† C_mul_X_pow_eq_monomial]
  -- âŠ¢ degree (â†‘C (coeff q n) * X ^ n) = degree (â†‘C s * X ^ n)
  simp only [hzero, hqzero, Ne.def, not_false_iff, degree_C_mul_X_pow]
  -- ğŸ‰ no goals
#align polynomial.monomial_mem_lifts_and_degree_eq Polynomial.monomial_mem_lifts_and_degree_eq

/-- A polynomial lifts if and only if it can be lifted to a polynomial of the same degree. -/
theorem mem_lifts_and_degree_eq {p : S[X]} (hlifts : p âˆˆ lifts f) :
    âˆƒ q : R[X], map f q = p âˆ§ q.degree = p.degree := by
  generalize hd : p.natDegree = d
  -- âŠ¢ âˆƒ q, map f q = p âˆ§ degree q = degree p
  revert hd p
  -- âŠ¢ âˆ€ {p : S[X]}, p âˆˆ lifts f â†’ natDegree p = d â†’ âˆƒ q, map f q = p âˆ§ degree q =  â€¦
  induction' d using Nat.strong_induction_on with n hn
  -- âŠ¢ âˆ€ {p : S[X]}, p âˆˆ lifts f â†’ natDegree p = n â†’ âˆƒ q, map f q = p âˆ§ degree q =  â€¦
  intros p hlifts hdeg
  -- âŠ¢ âˆƒ q, map f q = p âˆ§ degree q = degree p
  by_cases erase_zero : p.eraseLead = 0
  -- âŠ¢ âˆƒ q, map f q = p âˆ§ degree q = degree p
  Â· rw [â† eraseLead_add_monomial_natDegree_leadingCoeff p, erase_zero, zero_add, leadingCoeff]
    -- âŠ¢ âˆƒ q, map f q = â†‘(monomial (natDegree p)) (coeff p (natDegree p)) âˆ§ degree q  â€¦
    exact
      monomial_mem_lifts_and_degree_eq
        (monomial_mem_lifts p.natDegree ((lifts_iff_coeff_lifts p).1 hlifts p.natDegree))
  have deg_erase := Or.resolve_right (eraseLead_natDegree_lt_or_eraseLead_eq_zero p) erase_zero
  -- âŠ¢ âˆƒ q, map f q = p âˆ§ degree q = degree p
  have pzero : p â‰  0 := by
    intro habs
    exfalso
    rw [habs, eraseLead_zero, eq_self_iff_true, not_true] at erase_zero
    exact erase_zero
  have lead_zero : p.coeff p.natDegree â‰  0 := by
    rw [â† leadingCoeff, Ne.def, leadingCoeff_eq_zero]; exact pzero
  obtain âŸ¨lead, hleadâŸ© :=
    monomial_mem_lifts_and_degree_eq
      (monomial_mem_lifts p.natDegree ((lifts_iff_coeff_lifts p).1 hlifts p.natDegree))
  have deg_lead : lead.degree = p.natDegree := by
    rw [hlead.2, â† C_mul_X_pow_eq_monomial, degree_C_mul_X_pow p.natDegree lead_zero]
  rw [hdeg] at deg_erase
  -- âŠ¢ âˆƒ q, map f q = p âˆ§ degree q = degree p
  obtain âŸ¨erase, heraseâŸ© :=
    hn p.eraseLead.natDegree deg_erase (erase_mem_lifts p.natDegree hlifts)
      (refl p.eraseLead.natDegree)
  use erase + lead
  -- âŠ¢ map f (erase + lead) = p âˆ§ degree (erase + lead) = degree p
  constructor
  -- âŠ¢ map f (erase + lead) = p
  Â· simp only [hlead, herase, Polynomial.map_add]
    -- âŠ¢ Polynomial.erase (natDegree p) p + â†‘(monomial (natDegree p)) (coeff p (natDe â€¦
    rw [â†eraseLead, â†leadingCoeff]
    -- âŠ¢ eraseLead p + â†‘(monomial (natDegree p)) (leadingCoeff p) = p
    rw [eraseLead_add_monomial_natDegree_leadingCoeff p]
    -- ğŸ‰ no goals
  rw [degree_eq_natDegree pzero, â†deg_lead]
  -- âŠ¢ degree (erase + lead) = degree lead
  apply degree_add_eq_right_of_degree_lt
  -- âŠ¢ degree erase < degree lead
  rw [herase.2, deg_lead, â†degree_eq_natDegree pzero]
  -- âŠ¢ degree (Polynomial.erase (natDegree p) p) < degree p
  exact degree_erase_lt pzero
  -- ğŸ‰ no goals
#align polynomial.mem_lifts_and_degree_eq Polynomial.mem_lifts_and_degree_eq

end LiftDeg

section Monic

/-- A monic polynomial lifts if and only if it can be lifted to a monic polynomial
of the same degree. -/
theorem lifts_and_degree_eq_and_monic [Nontrivial S] {p : S[X]} (hlifts : p âˆˆ lifts f)
    (hp : p.Monic) : âˆƒ q : R[X], map f q = p âˆ§ q.degree = p.degree âˆ§ q.Monic := by
  cases' subsingleton_or_nontrivial R with hR hR
  -- âŠ¢ âˆƒ q, map f q = p âˆ§ degree q = degree p âˆ§ Monic q
  Â· obtain âŸ¨q, hqâŸ© := mem_lifts_and_degree_eq hlifts
    -- âŠ¢ âˆƒ q, map f q = p âˆ§ degree q = degree p âˆ§ Monic q
    exact âŸ¨q, hq.1, hq.2, monic_of_subsingleton _âŸ©
    -- ğŸ‰ no goals
  have H : erase p.natDegree p + X ^ p.natDegree = p := by
    simpa only [hp.leadingCoeff, C_1, one_mul, eraseLead] using eraseLead_add_C_mul_X_pow p
  by_cases h0 : erase p.natDegree p = 0
  -- âŠ¢ âˆƒ q, map f q = p âˆ§ degree q = degree p âˆ§ Monic q
  Â· rw [â† H, h0, zero_add]
    -- âŠ¢ âˆƒ q, map f q = X ^ natDegree p âˆ§ degree q = degree (X ^ natDegree p) âˆ§ Monic q
    refine' âŸ¨X ^ p.natDegree, _, _, monic_X_pow p.natDegreeâŸ©
    -- âŠ¢ map f (X ^ natDegree p) = X ^ natDegree p
    Â· rw [Polynomial.map_pow, map_X]
      -- ğŸ‰ no goals
    Â· rw [degree_X_pow, degree_X_pow]
      -- ğŸ‰ no goals
  obtain âŸ¨q, hqâŸ© := mem_lifts_and_degree_eq (erase_mem_lifts p.natDegree hlifts)
  -- âŠ¢ âˆƒ q, map f q = p âˆ§ degree q = degree p âˆ§ Monic q
  have p_neq_0 : p â‰  0 := by intro hp; apply h0; rw [hp]; simp only [natDegree_zero, erase_zero]
  -- âŠ¢ âˆƒ q, map f q = p âˆ§ degree q = degree p âˆ§ Monic q
  have hdeg : q.degree < (X ^ p.natDegree).degree := by
    rw [@degree_X_pow R, hq.2, â†degree_eq_natDegree p_neq_0]
    exact degree_erase_lt p_neq_0
  refine' âŸ¨q + X ^ p.natDegree, _, _, (monic_X_pow _).add_of_right hdegâŸ©
  -- âŠ¢ map f (q + X ^ natDegree p) = p
  Â· rw [Polynomial.map_add, hq.1, Polynomial.map_pow, map_X, H]
    -- ğŸ‰ no goals
  Â· rw [degree_add_eq_right_of_degree_lt hdeg, degree_X_pow, degree_eq_natDegree hp.ne_zero]
    -- ğŸ‰ no goals
#align polynomial.lifts_and_degree_eq_and_monic Polynomial.lifts_and_degree_eq_and_monic

theorem lifts_and_natDegree_eq_and_monic {p : S[X]} (hlifts : p âˆˆ lifts f) (hp : p.Monic) :
    âˆƒ q : R[X], map f q = p âˆ§ q.natDegree = p.natDegree âˆ§ q.Monic := by
  cases' subsingleton_or_nontrivial S with hR hR
  -- âŠ¢ âˆƒ q, map f q = p âˆ§ natDegree q = natDegree p âˆ§ Monic q
  Â· obtain rfl : p = 1 := Subsingleton.elim _ _
    -- âŠ¢ âˆƒ q, map f q = 1 âˆ§ natDegree q = natDegree 1 âˆ§ Monic q
    refine' âŸ¨1, Subsingleton.elim _ _, by simp, by simpâŸ©
    -- ğŸ‰ no goals
  obtain âŸ¨p', hâ‚, hâ‚‚, hâ‚ƒâŸ© := lifts_and_degree_eq_and_monic hlifts hp
  -- âŠ¢ âˆƒ q, map f q = p âˆ§ natDegree q = natDegree p âˆ§ Monic q
  exact âŸ¨p', hâ‚, natDegree_eq_of_degree_eq hâ‚‚, hâ‚ƒâŸ©
  -- ğŸ‰ no goals
#align polynomial.lifts_and_nat_degree_eq_and_monic Polynomial.lifts_and_natDegree_eq_and_monic

end Monic

end Semiring

section Ring

variable {R : Type u} [Ring R] {S : Type v} [Ring S] (f : R â†’+* S)

/-- The subring of polynomials that lift. -/
def liftsRing (f : R â†’+* S) : Subring S[X] :=
  RingHom.range (mapRingHom f)
#align polynomial.lifts_ring Polynomial.liftsRing

/-- If `R` and `S` are rings, `p` is in the subring of polynomials that lift if and only if it is in
the subsemiring of polynomials that lift. -/
theorem lifts_iff_liftsRing (p : S[X]) : p âˆˆ lifts f â†” p âˆˆ liftsRing f := by
  simp only [lifts, liftsRing, RingHom.mem_range, RingHom.mem_rangeS]
  -- ğŸ‰ no goals
#align polynomial.lifts_iff_lifts_ring Polynomial.lifts_iff_liftsRing

end Ring

section Algebra

variable {R : Type u} [CommSemiring R] {S : Type v} [Semiring S] [Algebra R S]

/-- The map `R[X] â†’ S[X]` as an algebra homomorphism. -/
def mapAlg (R : Type u) [CommSemiring R] (S : Type v) [Semiring S] [Algebra R S] :
    R[X] â†’â‚[R] S[X] :=
  @aeval _ S[X] _ _ _ (X : S[X])
#align polynomial.map_alg Polynomial.mapAlg

/-- `mapAlg` is the morphism induced by `R â†’ S`. -/
theorem mapAlg_eq_map (p : R[X]) : mapAlg R S p = map (algebraMap R S) p := by
  simp only [mapAlg, aeval_def, evalâ‚‚_eq_sum, map, algebraMap_apply, RingHom.coe_comp]
  -- âŠ¢ (sum p fun e a => â†‘C (â†‘(algebraMap R S) a) * X ^ e) = sum p fun e a => (â†‘C âˆ˜ â€¦
  ext; congr
  -- âŠ¢ coeff (sum p fun e a => â†‘C (â†‘(algebraMap R S) a) * X ^ e) nâœ = coeff (sum p  â€¦
       -- ğŸ‰ no goals
#align polynomial.map_alg_eq_map Polynomial.mapAlg_eq_map

/-- A polynomial `p` lifts if and only if it is in the image of `mapAlg`. -/
theorem mem_lifts_iff_mem_alg (R : Type u) [CommSemiring R] {S : Type v} [Semiring S] [Algebra R S]
    (p : S[X]) : p âˆˆ lifts (algebraMap R S) â†” p âˆˆ AlgHom.range (@mapAlg R _ S _ _) := by
  simp only [coe_mapRingHom, lifts, mapAlg_eq_map, AlgHom.mem_range, RingHom.mem_rangeS]
  -- ğŸ‰ no goals
#align polynomial.mem_lifts_iff_mem_alg Polynomial.mem_lifts_iff_mem_alg

/-- If `p` lifts and `(r : R)` then `r â€¢ p` lifts. -/
theorem smul_mem_lifts {p : S[X]} (r : R) (hp : p âˆˆ lifts (algebraMap R S)) :
    r â€¢ p âˆˆ lifts (algebraMap R S) := by
  rw [mem_lifts_iff_mem_alg] at hp âŠ¢
  -- âŠ¢ r â€¢ p âˆˆ AlgHom.range (mapAlg R S)
  exact Subalgebra.smul_mem (mapAlg R S).range hp r
  -- ğŸ‰ no goals
#align polynomial.smul_mem_lifts Polynomial.smul_mem_lifts

end Algebra

end Polynomial
