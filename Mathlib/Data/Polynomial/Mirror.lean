/-
Copyright (c) 2020 Thomas Browning. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Thomas Browning
-/
import Mathlib.Algebra.BigOperators.NatAntidiagonal
import Mathlib.Data.Polynomial.RingDivision

#align_import data.polynomial.mirror from "leanprover-community/mathlib"@"2196ab363eb097c008d4497125e0dde23fb36db2"

/-!
# "Mirror" of a univariate polynomial

In this file we define `Polynomial.mirror`, a variant of `Polynomial.reverse`. The difference
between `reverse` and `mirror` is that `reverse` will decrease the degree if the polynomial is
divisible by `X`.

## Main definitions

- `Polynomial.mirror`

## Main results

- `Polynomial.mirror_mul_of_domain`: `mirror` preserves multiplication.
- `Polynomial.irreducible_of_mirror`: an irreducibility criterion involving `mirror`

-/


namespace Polynomial

open Polynomial

section Semiring

variable {R : Type*} [Semiring R] (p q : R[X])

/-- mirror of a polynomial: reverses the coefficients while preserving `Polynomial.natDegree` -/
noncomputable def mirror :=
  p.reverse * X ^ p.natTrailingDegree
#align polynomial.mirror Polynomial.mirror

@[simp]
theorem mirror_zero : (0 : R[X]).mirror = 0 := by simp [mirror]
                                                  -- ğŸ‰ no goals
#align polynomial.mirror_zero Polynomial.mirror_zero

theorem mirror_monomial (n : â„•) (a : R) : (monomial n a).mirror = monomial n a := by
  classical
    by_cases ha : a = 0
    Â· rw [ha, monomial_zero_right, mirror_zero]
    Â· rw [mirror, reverse, natDegree_monomial n a, if_neg ha, natTrailingDegree_monomial ha, â†
        C_mul_X_pow_eq_monomial, reflect_C_mul_X_pow, revAt_le (le_refl n), tsub_self, pow_zero,
        mul_one]
#align polynomial.mirror_monomial Polynomial.mirror_monomial

theorem mirror_C (a : R) : (C a).mirror = C a :=
  mirror_monomial 0 a
set_option linter.uppercaseLean3 false in
#align polynomial.mirror_C Polynomial.mirror_C

theorem mirror_X : X.mirror = (X : R[X]) :=
  mirror_monomial 1 (1 : R)
set_option linter.uppercaseLean3 false in
#align polynomial.mirror_X Polynomial.mirror_X

theorem mirror_natDegree : p.mirror.natDegree = p.natDegree := by
  by_cases hp : p = 0
  -- âŠ¢ natDegree (mirror p) = natDegree p
  Â· rw [hp, mirror_zero]
    -- ğŸ‰ no goals
  nontriviality R
  -- âŠ¢ natDegree (mirror p) = natDegree p
  rw [mirror, natDegree_mul', reverse_natDegree, natDegree_X_pow,
    tsub_add_cancel_of_le p.natTrailingDegree_le_natDegree]
  rwa [leadingCoeff_X_pow, mul_one, reverse_leadingCoeff, Ne, trailingCoeff_eq_zero]
  -- ğŸ‰ no goals
#align polynomial.mirror_nat_degree Polynomial.mirror_natDegree

theorem mirror_natTrailingDegree : p.mirror.natTrailingDegree = p.natTrailingDegree := by
  by_cases hp : p = 0
  -- âŠ¢ natTrailingDegree (mirror p) = natTrailingDegree p
  Â· rw [hp, mirror_zero]
    -- ğŸ‰ no goals
  Â· rw [mirror, natTrailingDegree_mul_X_pow ((mt reverse_eq_zero.mp) hp),
      reverse_natTrailingDegree, zero_add]
#align polynomial.mirror_nat_trailing_degree Polynomial.mirror_natTrailingDegree

theorem coeff_mirror (n : â„•) :
    p.mirror.coeff n = p.coeff (revAt (p.natDegree + p.natTrailingDegree) n) := by
  by_cases h2 : p.natDegree < n
  -- âŠ¢ coeff (mirror p) n = coeff p (â†‘(revAt (natDegree p + natTrailingDegree p)) n)
  Â· rw [coeff_eq_zero_of_natDegree_lt (by rwa [mirror_natDegree])]
    -- âŠ¢ 0 = coeff p (â†‘(revAt (natDegree p + natTrailingDegree p)) n)
    by_cases h1 : n â‰¤ p.natDegree + p.natTrailingDegree
    -- âŠ¢ 0 = coeff p (â†‘(revAt (natDegree p + natTrailingDegree p)) n)
    Â· rw [revAt_le h1, coeff_eq_zero_of_lt_natTrailingDegree]
      -- âŠ¢ natDegree p + natTrailingDegree p - n < natTrailingDegree p
      exact (tsub_lt_iff_left h1).mpr (Nat.add_lt_add_right h2 _)
      -- ğŸ‰ no goals
    Â· rw [â† revAtFun_eq, revAtFun, if_neg h1, coeff_eq_zero_of_natDegree_lt h2]
      -- ğŸ‰ no goals
  rw [not_lt] at h2
  -- âŠ¢ coeff (mirror p) n = coeff p (â†‘(revAt (natDegree p + natTrailingDegree p)) n)
  rw [revAt_le (h2.trans (Nat.le_add_right _ _))]
  -- âŠ¢ coeff (mirror p) n = coeff p (natDegree p + natTrailingDegree p - n)
  by_cases h3 : p.natTrailingDegree â‰¤ n
  -- âŠ¢ coeff (mirror p) n = coeff p (natDegree p + natTrailingDegree p - n)
  Â· rw [â† tsub_add_eq_add_tsub h2, â† tsub_tsub_assoc h2 h3, mirror, coeff_mul_X_pow', if_pos h3,
      coeff_reverse, revAt_le (tsub_le_self.trans h2)]
  rw [not_le] at h3
  -- âŠ¢ coeff (mirror p) n = coeff p (natDegree p + natTrailingDegree p - n)
  rw [coeff_eq_zero_of_natDegree_lt (lt_tsub_iff_right.mpr (Nat.add_lt_add_left h3 _))]
  -- âŠ¢ coeff (mirror p) n = 0
  exact coeff_eq_zero_of_lt_natTrailingDegree (by rwa [mirror_natTrailingDegree])
  -- ğŸ‰ no goals
#align polynomial.coeff_mirror Polynomial.coeff_mirror

--TODO: Extract `Finset.sum_range_rev_at` lemma.
theorem mirror_eval_one : p.mirror.eval 1 = p.eval 1 := by
  simp_rw [eval_eq_sum_range, one_pow, mul_one, mirror_natDegree]
  -- âŠ¢ (Finset.sum (Finset.range (natDegree p + 1)) fun x => coeff (mirror p) x) =  â€¦
  refine' Finset.sum_bij_ne_zero _ _ _ _ _
  Â· exact fun n _ _ => revAt (p.natDegree + p.natTrailingDegree) n
    -- ğŸ‰ no goals
  Â· intro n hn hp
    -- âŠ¢ â†‘(revAt (natDegree p + natTrailingDegree p)) n âˆˆ Finset.range (natDegree p + â€¦
    rw [Finset.mem_range_succ_iff] at *
    -- âŠ¢ â†‘(revAt (natDegree p + natTrailingDegree p)) n â‰¤ natDegree p
    rw [revAt_le (hn.trans (Nat.le_add_right _ _))]
    -- âŠ¢ natDegree p + natTrailingDegree p - n â‰¤ natDegree p
    rw [tsub_le_iff_tsub_le, add_comm, add_tsub_cancel_right, â† mirror_natTrailingDegree]
    -- âŠ¢ natTrailingDegree (mirror p) â‰¤ n
    exact natTrailingDegree_le_of_ne_zero hp
    -- ğŸ‰ no goals
  Â· exact fun nâ‚ nâ‚‚ _ _ _ _ h => by rw [â† @revAt_invol _ nâ‚, h, revAt_invol]
    -- ğŸ‰ no goals
  Â· intro n hn hp
    -- âŠ¢ âˆƒ a hâ‚ hâ‚‚, n = â†‘(revAt (natDegree p + natTrailingDegree p)) a
    use revAt (p.natDegree + p.natTrailingDegree) n
    -- âŠ¢ âˆƒ hâ‚ hâ‚‚, n = â†‘(revAt (natDegree p + natTrailingDegree p)) (â†‘(revAt (natDegre â€¦
    refine' âŸ¨_, _, revAt_invol.symmâŸ©
    -- âŠ¢ â†‘(revAt (natDegree p + natTrailingDegree p)) n âˆˆ Finset.range (natDegree p + â€¦
    Â· rw [Finset.mem_range_succ_iff] at *
      -- âŠ¢ â†‘(revAt (natDegree p + natTrailingDegree p)) n â‰¤ natDegree p
      rw [revAt_le (hn.trans (Nat.le_add_right _ _))]
      -- âŠ¢ natDegree p + natTrailingDegree p - n â‰¤ natDegree p
      rw [tsub_le_iff_tsub_le, add_comm, add_tsub_cancel_right]
      -- âŠ¢ natTrailingDegree p â‰¤ n
      exact natTrailingDegree_le_of_ne_zero hp
      -- ğŸ‰ no goals
    Â· change p.mirror.coeff _ â‰  0
      -- âŠ¢ coeff (mirror p) (â†‘(revAt (natDegree p + natTrailingDegree p)) n) â‰  0
      rwa [coeff_mirror, revAt_invol]
      -- ğŸ‰ no goals
  Â· exact fun n _ _ => p.coeff_mirror n
    -- ğŸ‰ no goals
#align polynomial.mirror_eval_one Polynomial.mirror_eval_one

theorem mirror_mirror : p.mirror.mirror = p :=
  Polynomial.ext fun n => by
    rw [coeff_mirror, coeff_mirror, mirror_natDegree, mirror_natTrailingDegree, revAt_invol]
    -- ğŸ‰ no goals
#align polynomial.mirror_mirror Polynomial.mirror_mirror

variable {p q}

theorem mirror_involutive : Function.Involutive (mirror : R[X] â†’ R[X]) :=
  mirror_mirror
#align polynomial.mirror_involutive Polynomial.mirror_involutive

theorem mirror_eq_iff : p.mirror = q â†” p = q.mirror :=
  mirror_involutive.eq_iff
#align polynomial.mirror_eq_iff Polynomial.mirror_eq_iff

@[simp]
theorem mirror_inj : p.mirror = q.mirror â†” p = q :=
  mirror_involutive.injective.eq_iff
#align polynomial.mirror_inj Polynomial.mirror_inj

@[simp]
theorem mirror_eq_zero : p.mirror = 0 â†” p = 0 :=
  âŸ¨fun h => by rw [â† p.mirror_mirror, h, mirror_zero], fun h => by rw [h, mirror_zero]âŸ©
               -- ğŸ‰ no goals
                                                                   -- ğŸ‰ no goals
#align polynomial.mirror_eq_zero Polynomial.mirror_eq_zero

variable (p q)

@[simp]
theorem mirror_trailingCoeff : p.mirror.trailingCoeff = p.leadingCoeff := by
  rw [leadingCoeff, trailingCoeff, mirror_natTrailingDegree, coeff_mirror,
    revAt_le (Nat.le_add_left _ _), add_tsub_cancel_right]
#align polynomial.mirror_trailing_coeff Polynomial.mirror_trailingCoeff

@[simp]
theorem mirror_leadingCoeff : p.mirror.leadingCoeff = p.trailingCoeff := by
  rw [â† p.mirror_mirror, mirror_trailingCoeff, p.mirror_mirror]
  -- ğŸ‰ no goals
#align polynomial.mirror_leading_coeff Polynomial.mirror_leadingCoeff

theorem coeff_mul_mirror :
    (p * p.mirror).coeff (p.natDegree + p.natTrailingDegree) = p.sum fun n => (Â· ^ 2) := by
  rw [coeff_mul, Finset.Nat.sum_antidiagonal_eq_sum_range_succ_mk]
  -- âŠ¢ (Finset.sum (Finset.range (Nat.succ (natDegree p + natTrailingDegree p))) fu â€¦
  refine'
    (Finset.sum_congr rfl fun n hn => _).trans
      (p.sum_eq_of_subset (fun _ => (Â· ^ 2)) (fun _ => zero_pow zero_lt_two) fun n hn =>
          Finset.mem_range_succ_iff.mpr
            ((le_natDegree_of_mem_supp n hn).trans (Nat.le_add_right _ _))).symm
  rw [coeff_mirror, â† revAt_le (Finset.mem_range_succ_iff.mp hn), revAt_invol, â† sq]
  -- ğŸ‰ no goals
#align polynomial.coeff_mul_mirror Polynomial.coeff_mul_mirror

variable [NoZeroDivisors R]

theorem natDegree_mul_mirror : (p * p.mirror).natDegree = 2 * p.natDegree := by
  by_cases hp : p = 0
  -- âŠ¢ natDegree (p * mirror p) = 2 * natDegree p
  Â· rw [hp, zero_mul, natDegree_zero, mul_zero]
    -- ğŸ‰ no goals
  rw [natDegree_mul hp (mt mirror_eq_zero.mp hp), mirror_natDegree, two_mul]
  -- ğŸ‰ no goals
#align polynomial.nat_degree_mul_mirror Polynomial.natDegree_mul_mirror

theorem natTrailingDegree_mul_mirror :
    (p * p.mirror).natTrailingDegree = 2 * p.natTrailingDegree := by
  by_cases hp : p = 0
  -- âŠ¢ natTrailingDegree (p * mirror p) = 2 * natTrailingDegree p
  Â· rw [hp, zero_mul, natTrailingDegree_zero, mul_zero]
    -- ğŸ‰ no goals
  rw [natTrailingDegree_mul hp (mt mirror_eq_zero.mp hp), mirror_natTrailingDegree, two_mul]
  -- ğŸ‰ no goals
#align polynomial.nat_trailing_degree_mul_mirror Polynomial.natTrailingDegree_mul_mirror

end Semiring

section Ring

variable {R : Type*} [Ring R] (p q : R[X])

theorem mirror_neg : (-p).mirror = -p.mirror := by
  rw [mirror, mirror, reverse_neg, natTrailingDegree_neg, neg_mul_eq_neg_mul]
  -- ğŸ‰ no goals
#align polynomial.mirror_neg Polynomial.mirror_neg

variable [NoZeroDivisors R]

theorem mirror_mul_of_domain : (p * q).mirror = p.mirror * q.mirror := by
  by_cases hp : p = 0
  -- âŠ¢ mirror (p * q) = mirror p * mirror q
  Â· rw [hp, zero_mul, mirror_zero, zero_mul]
    -- ğŸ‰ no goals
  by_cases hq : q = 0
  -- âŠ¢ mirror (p * q) = mirror p * mirror q
  Â· rw [hq, mul_zero, mirror_zero, mul_zero]
    -- ğŸ‰ no goals
  rw [mirror, mirror, mirror, reverse_mul_of_domain, natTrailingDegree_mul hp hq, pow_add]
  -- âŠ¢ reverse p * reverse q * (X ^ natTrailingDegree p * X ^ natTrailingDegree q)  â€¦
  rw [mul_assoc, â† mul_assoc q.reverse, â† X_pow_mul (p := reverse q)]
  -- âŠ¢ reverse p * (X ^ natTrailingDegree p * reverse q * X ^ natTrailingDegree q)  â€¦
  repeat' rw [mul_assoc]
  -- ğŸ‰ no goals
#align polynomial.mirror_mul_of_domain Polynomial.mirror_mul_of_domain

theorem mirror_smul (a : R) : (a â€¢ p).mirror = a â€¢ p.mirror := by
  rw [â† C_mul', â† C_mul', mirror_mul_of_domain, mirror_C]
  -- ğŸ‰ no goals
#align polynomial.mirror_smul Polynomial.mirror_smul

end Ring

section CommRing

variable {R : Type*} [CommRing R] [NoZeroDivisors R] {f : R[X]}

theorem irreducible_of_mirror (h1 : Â¬IsUnit f)
    (h2 : âˆ€ k, f * f.mirror = k * k.mirror â†’ k = f âˆ¨ k = -f âˆ¨ k = f.mirror âˆ¨ k = -f.mirror)
    (h3 : âˆ€ g, g âˆ£ f â†’ g âˆ£ f.mirror â†’ IsUnit g) : Irreducible f := by
  constructor
  -- âŠ¢ Â¬IsUnit f
  Â· exact h1
    -- ğŸ‰ no goals
  Â· intro g h fgh
    -- âŠ¢ IsUnit g âˆ¨ IsUnit h
    let k := g * h.mirror
    -- âŠ¢ IsUnit g âˆ¨ IsUnit h
    have key : f * f.mirror = k * k.mirror := by
      rw [fgh, mirror_mul_of_domain, mirror_mul_of_domain, mirror_mirror, mul_assoc, mul_comm h,
        mul_comm g.mirror, mul_assoc, â† mul_assoc]
    have g_dvd_f : g âˆ£ f := by
      rw [fgh]
      exact dvd_mul_right g h
    have h_dvd_f : h âˆ£ f := by
      rw [fgh]
      exact dvd_mul_left h g
    have g_dvd_k : g âˆ£ k := dvd_mul_right g h.mirror
    -- âŠ¢ IsUnit g âˆ¨ IsUnit h
    have h_dvd_k_rev : h âˆ£ k.mirror := by
      rw [mirror_mul_of_domain, mirror_mirror]
      exact dvd_mul_left h g.mirror
    have hk := h2 k key
    -- âŠ¢ IsUnit g âˆ¨ IsUnit h
    rcases hk with (hk | hk | hk | hk)
    Â· exact Or.inr (h3 h h_dvd_f (by rwa [â† hk]))
      -- ğŸ‰ no goals
    Â· exact Or.inr (h3 h h_dvd_f (by rwa [â† neg_eq_iff_eq_neg.mpr hk, mirror_neg, dvd_neg]))
      -- ğŸ‰ no goals
    Â· exact Or.inl (h3 g g_dvd_f (by rwa [â† hk]))
      -- ğŸ‰ no goals
    Â· exact Or.inl (h3 g g_dvd_f (by rwa [â† neg_eq_iff_eq_neg.mpr hk, dvd_neg]))
      -- ğŸ‰ no goals
#align polynomial.irreducible_of_mirror Polynomial.irreducible_of_mirror

end CommRing

end Polynomial
