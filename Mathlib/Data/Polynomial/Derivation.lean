/-
Copyright (c) 2023 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Richard Hill
-/
import Mathlib.Data.Polynomial.Derivative
import Mathlib.RingTheory.Derivation.Basic
import Mathlib.Data.Polynomial.AlgebraMap

/-!
# Derivations of univariate polynomials

In this file we prove that an `R`-derivation of `Polynomial R` is determined by its value on `X`.
We also provide a constructor `Polynomial.mkDerivation` that
builds a derivation from its value on `X`, and a linear equivalence
`Polynomial.mkDerivationEquiv` between `A` and `Derivation (Polynomial R) A`.
-/

noncomputable section

namespace Polynomial

open scoped BigOperators

variable {R A : Type*} [CommSemiring R]

/-- `Polynomial.derivative` as a derivation. -/
@[simps]
def derivative' : Derivation R R[X] R[X] where
  toFun := derivative
  map_add' _ _ := derivative_add
  map_smul' := derivative_smul
  map_one_eq_zero' := derivative_one
  leibniz' f g := by simp [mul_comm, add_comm, derivative_mul]
                     -- ðŸŽ‰ no goals

variable [AddCommMonoid A] [Module R A] [Module (Polynomial R) A]

@[simp]
theorem derivation_C (D : Derivation R R[X] A) (a : R) : D (C a) = 0 :=
  D.map_algebraMap a

@[simp]
theorem C_smul_derivation_apply (D : Derivation R R[X] A) (a : R) (f : R[X]) :
    C a â€¢ D f = a â€¢ D f := by
  have : C a â€¢ D f = D (C a * f) := by simp
  -- âŠ¢ â†‘C a â€¢ â†‘D f = a â€¢ â†‘D f
  rw [this, C_mul', D.map_smul]
  -- ðŸŽ‰ no goals

@[ext]
theorem derivation_ext {Dâ‚ Dâ‚‚ : Derivation R R[X] A} (h : Dâ‚ X = Dâ‚‚ X) : Dâ‚ = Dâ‚‚ :=
  Derivation.ext fun f => Derivation.eqOn_adjoin (Set.eqOn_singleton.2 h) <| by
    simp only [adjoin_X, Algebra.coe_top, Set.mem_univ]
    -- ðŸŽ‰ no goals

variable [IsScalarTower R (Polynomial R) A]

variable (R)

/-- The derivation on `R[X]` that takes the value `a` on `X`. -/
def mkDerivation : A â†’â‚—[R] Derivation R R[X] A where
  toFun := fun a â†¦ (LinearMap.toSpanSingleton R[X] A a).compDer derivative'
  map_add' := fun a b â†¦ by ext; simp
                           -- âŠ¢ â†‘((fun a => â†‘(LinearMap.compDer (LinearMap.toSpanSingleton R[X] A a)) deriva â€¦
                                -- ðŸŽ‰ no goals
  map_smul' := fun t a â†¦ by ext; simp
                            -- âŠ¢ â†‘(AddHom.toFun { toFun := fun a => â†‘(LinearMap.compDer (LinearMap.toSpanSing â€¦
                                 -- ðŸŽ‰ no goals

lemma mkDerivation_apply (a : A) (f : R[X]) :
    mkDerivation R a f = derivative f â€¢ a := by
  rfl
  -- ðŸŽ‰ no goals

@[simp]
theorem mkDerivation_X (a : A) : mkDerivation R a X = a := by simp [mkDerivation_apply]
                                                              -- ðŸŽ‰ no goals

lemma mkDerivation_one_eq_derivative' : mkDerivation R (1 : R[X]) = derivative' := by
  ext : 1
  -- âŠ¢ â†‘(â†‘(mkDerivation R) 1) X = â†‘derivative' X
  simp [derivative']
  -- ðŸŽ‰ no goals

lemma mkDerivation_one_eq_derivative (f : R[X]) : mkDerivation R (1 : R[X]) f = derivative f := by
  rw [mkDerivation_one_eq_derivative']
  -- âŠ¢ â†‘derivative' f = â†‘derivative f
  rfl
  -- ðŸŽ‰ no goals

/-- `Polynomial.mkDerivation` as a linear equivalence. -/
def mkDerivationEquiv : A â‰ƒâ‚—[R] Derivation R R[X] A :=
  LinearEquiv.symm <|
    { invFun := mkDerivation R
      toFun := fun D => D X
      map_add' := fun _ _ => rfl
      map_smul' := fun _ _ => rfl
      left_inv := fun _ => derivation_ext <| mkDerivation_X _ _
      right_inv := fun _ => mkDerivation_X _ _ }

@[simp] lemma mkDerivationEquiv_apply (a : A) :
    mkDerivationEquiv R a = mkDerivation R a := by
  rfl
  -- ðŸŽ‰ no goals

@[simp] lemma mkDerivationEquiv_symm_apply (D : Derivation R R[X] A) :
    (mkDerivationEquiv R).symm D = D X := rfl

end Polynomial
