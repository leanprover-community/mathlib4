/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Johannes H√∂lzl, Scott Morrison, Jens Wagemaker
-/
import Mathlib.Data.Polynomial.Basic

#align_import data.polynomial.monomial from "leanprover-community/mathlib"@"220f71ba506c8958c9b41bd82226b3d06b0991e8"

/-!
# Univariate monomials

Preparatory lemmas for degree_basic.
-/


noncomputable section

namespace Polynomial

open Polynomial

universe u

variable {R : Type u} {a b : R} {m n : ‚Ñï}

variable [Semiring R] {p q r : R[X]}

theorem monomial_one_eq_iff [Nontrivial R] {i j : ‚Ñï} :
    (monomial i 1 : R[X]) = monomial j 1 ‚Üî i = j := by
  -- Porting note: `ofFinsupp.injEq` is required.
  simp_rw [‚Üê ofFinsupp_single, ofFinsupp.injEq]
  -- ‚ä¢ Finsupp.single i 1 = Finsupp.single j 1 ‚Üî i = j
  exact AddMonoidAlgebra.of_injective.eq_iff
  -- üéâ no goals
#align polynomial.monomial_one_eq_iff Polynomial.monomial_one_eq_iff

instance infinite [Nontrivial R] : Infinite R[X] :=
  Infinite.of_injective (fun i => monomial i 1) fun m n h => by simpa [monomial_one_eq_iff] using h
                                                                -- üéâ no goals
#align polynomial.infinite Polynomial.infinite

theorem card_support_le_one_iff_monomial {f : R[X]} :
    Finset.card f.support ‚â§ 1 ‚Üî ‚àÉ n a, f = monomial n a := by
  constructor
  -- ‚ä¢ Finset.card (support f) ‚â§ 1 ‚Üí ‚àÉ n a, f = ‚Üë(monomial n) a
  ¬∑ intro H
    -- ‚ä¢ ‚àÉ n a, f = ‚Üë(monomial n) a
    rw [Finset.card_le_one_iff_subset_singleton] at H
    -- ‚ä¢ ‚àÉ n a, f = ‚Üë(monomial n) a
    rcases H with ‚ü®n, hn‚ü©
    -- ‚ä¢ ‚àÉ n a, f = ‚Üë(monomial n) a
    refine' ‚ü®n, f.coeff n, _‚ü©
    -- ‚ä¢ f = ‚Üë(monomial n) (coeff f n)
    ext i
    -- ‚ä¢ coeff f i = coeff (‚Üë(monomial n) (coeff f n)) i
    by_cases hi : i = n
    -- ‚ä¢ coeff f i = coeff (‚Üë(monomial n) (coeff f n)) i
    ¬∑ simp [hi, coeff_monomial]
      -- üéâ no goals
    ¬∑ have : f.coeff i = 0 := by
        rw [‚Üê not_mem_support_iff]
        exact fun hi' => hi (Finset.mem_singleton.1 (hn hi'))
      simp [this, Ne.symm hi, coeff_monomial]
      -- üéâ no goals
  ¬∑ rintro ‚ü®n, a, rfl‚ü©
    -- ‚ä¢ Finset.card (support (‚Üë(monomial n) a)) ‚â§ 1
    rw [‚Üê Finset.card_singleton n]
    -- ‚ä¢ Finset.card (support (‚Üë(monomial n) a)) ‚â§ Finset.card {n}
    apply Finset.card_le_of_subset
    -- ‚ä¢ support (‚Üë(monomial n) a) ‚äÜ {n}
    exact support_monomial' _ _
    -- üéâ no goals
#align polynomial.card_support_le_one_iff_monomial Polynomial.card_support_le_one_iff_monomial

theorem ringHom_ext {S} [Semiring S] {f g : R[X] ‚Üí+* S} (h‚ÇÅ : ‚àÄ a, f (C a) = g (C a))
    (h‚ÇÇ : f X = g X) : f = g := by
  set f' := f.comp (toFinsuppIso R).symm.toRingHom with hf'
  -- ‚ä¢ f = g
  set g' := g.comp (toFinsuppIso R).symm.toRingHom with hg'
  -- ‚ä¢ f = g
  have A : f' = g' := by
    -- Porting note: Was `ext; simp [..]; simpa [..] using h‚ÇÇ`.
    ext : 1
    ¬∑ ext
      simp [h‚ÇÅ, RingEquiv.toRingHom_eq_coe]
    ¬∑ refine MonoidHom.ext_mnat ?_
      simpa [RingEquiv.toRingHom_eq_coe] using h‚ÇÇ
  have B : f = f'.comp (toFinsuppIso R) := by
    rw [hf', RingHom.comp_assoc]
    ext x
    simp only [RingEquiv.toRingHom_eq_coe, RingEquiv.symm_apply_apply, Function.comp_apply,
      RingHom.coe_comp, RingEquiv.coe_toRingHom]
  have C' : g = g'.comp (toFinsuppIso R) := by
    rw [hg', RingHom.comp_assoc]
    ext x
    simp only [RingEquiv.toRingHom_eq_coe, RingEquiv.symm_apply_apply, Function.comp_apply,
      RingHom.coe_comp, RingEquiv.coe_toRingHom]
  rw [B, C', A]
  -- üéâ no goals
#align polynomial.ring_hom_ext Polynomial.ringHom_ext

@[ext high]
theorem ringHom_ext' {S} [Semiring S] {f g : R[X] ‚Üí+* S} (h‚ÇÅ : f.comp C = g.comp C)
    (h‚ÇÇ : f X = g X) : f = g :=
  ringHom_ext (RingHom.congr_fun h‚ÇÅ) h‚ÇÇ
#align polynomial.ring_hom_ext' Polynomial.ringHom_ext'

end Polynomial
