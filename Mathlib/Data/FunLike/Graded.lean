/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/
import Mathlib.Data.SetLike.Basic

/-! # Class of grading-preserving functions and isomorphisms

We define `GradedFunLike F ğ’œ â„¬` where `ğ’œ` and `â„¬` represent some sort of grading. This class
extends `FunLike A B` where `A` and `B` are the underlying types.

We also define `GradedEquivLike E ğ’œ â„¬`, which is similar to `EquivLike`, where here `e : E` is
required to satisfy `x âˆˆ ğ’œ i â†” e x âˆˆ â„¬ i`.
-/

/-- The class `GradedFunLike F ğ’œ â„¬` expresses that terms of type `F` have an injective coercion to
grading-preserving functions from `A` to `B`, where `ğ’œ` is a grading on `A` and `â„¬` is a grading on
`B`. This typeclass extends `FunLike F A B`, so it is **not** necessary to repeat `[FunLike F A B]`
in the assumptions. This typeclass is used in the charactersation of certain types of graded
homomorphisms, such as `GradedRingHom` and `GradedAlgHom`. For example, what would be called
`"GradedRingHomClass F ğ’œ â„¬`" would be expressed as `[GradedFunLike F ğ’œ â„¬] [RingHomClass F A B]`.
-/
class GradedFunLike (F : Type*) {A B Ïƒ Ï„ Î¹ : outParam Type*}
    [SetLike Ïƒ A] [SetLike Ï„ B] (ğ’œ : outParam <| Î¹ â†’ Ïƒ) (â„¬ : outParam <| Î¹ â†’ Ï„)
    extends FunLike F A B where
  map_mem (f : F) {i x} : x âˆˆ ğ’œ i â†’ f x âˆˆ â„¬ i

section GradedFunLike

attribute [instance 100] GradedFunLike.toDFunLike

variable {F A B Ïƒ Ï„ Î¹ : Type*}
  [SetLike Ïƒ A] [SetLike Ï„ B] {ğ’œ : Î¹ â†’ Ïƒ} {â„¬ : Î¹ â†’ Ï„} [GradedFunLike F ğ’œ â„¬]

lemma map_mem (f : F) {i x} (h : x âˆˆ ğ’œ i) : f x âˆˆ â„¬ i :=
  GradedFunLike.map_mem f h

/-- A graded map descends to a map on each component. -/
def mapGraded (f : F) (i : Î¹) (x : ğ’œ i) : â„¬ i :=
  âŸ¨f x, map_mem f x.2âŸ©

end GradedFunLike

/-- The class `GradedEquivLike E ğ’œ â„¬` says that `E` is a type of grading-preserving isomorphisms
between `ğ’œ` and `â„¬`. It is the combination of `GradedFunLike E ğ’œ â„¬` and `EquivLike E A B`. -/
class GradedEquivLike (E : Type*) {A B Ïƒ Ï„ Î¹ : outParam Type*}
    [SetLike Ïƒ A] [SetLike Ï„ B] (ğ’œ : outParam <| Î¹ â†’ Ïƒ) (â„¬ : outParam <| Î¹ â†’ Ï„)
    extends EquivLike E A B where
  map_mem_iff (e : E) {i x} : e x âˆˆ â„¬ i â†” x âˆˆ ğ’œ i

section GradedEquivLike

attribute [instance 100] GradedEquivLike.toEquivLike

variable (E : Type*) {A B Ïƒ Ï„ Î¹ : Type*} [SetLike Ïƒ A] [SetLike Ï„ B]
  (ğ’œ : Î¹ â†’ Ïƒ) (â„¬ : Î¹ â†’ Ï„) [GradedEquivLike E ğ’œ â„¬]

instance (priority := 100) GradedEquivLike.toGradedFunLike : GradedFunLike E ğ’œ â„¬ where
  __ := inferInstanceAs (FunLike E A B)
  map_mem e {_ _} := (map_mem_iff e).mpr

variable {E ğ’œ â„¬}

lemma map_mem_iff (e : E) {i x} : e x âˆˆ â„¬ i â†” x âˆˆ ğ’œ i :=
  GradedEquivLike.map_mem_iff e
alias âŸ¨mem_of_map_mem, map_mem_of_memâŸ© := map_mem_iff

/-- A graded isomorphism descends to an isomorphism on each component. -/
@[simps] def equivGraded (e : E) (i : Î¹) : ğ’œ i â‰ƒ â„¬ i where
  toFun := mapGraded e i
  invFun y := âŸ¨EquivLike.inv e (y : B),
    mem_of_map_mem e <| by rw [EquivLike.apply_inv_apply]; exact y.2âŸ©
  left_inv _ := by ext; exact EquivLike.inv_apply_apply e _
  right_inv _ := by ext; exact EquivLike.apply_inv_apply e _

end GradedEquivLike
