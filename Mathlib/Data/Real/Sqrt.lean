/-
Copyright (c) 2020 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Floris van Doorn, Yury Kudryashov
-/
import Mathlib.Algebra.Star.Order
import Mathlib.Topology.Algebra.Order.MonotoneContinuity
import Mathlib.Topology.Instances.NNReal
import Mathlib.Tactic.Positivity

#align_import data.real.sqrt from "leanprover-community/mathlib"@"31c24aa72e7b3e5ed97a8412470e904f82b81004"

/-!
# Square root of a real number

In this file we define

* `NNReal.sqrt` to be the square root of a nonnegative real number.
* `Real.sqrt` to be the square root of a real number, defined to be zero on negative numbers.

Then we prove some basic properties of these functions.

## Implementation notes

We define `NNReal.sqrt` as the noncomputable inverse to the function `x ‚Ü¶ x * x`. We use general
theory of inverses of strictly monotone functions to prove that `NNReal.sqrt x` exists. As a side
effect, `NNReal.sqrt` is a bundled `OrderIso`, so for `NNReal` numbers we get continuity as well as
theorems like `NNReal.sqrt x ‚â§ y ‚Üî x ‚â§ y * y` for free.

Then we define `Real.sqrt x` to be `NNReal.sqrt (Real.toNNReal x)`. We also define a Cauchy sequence
`Real.sqrtAux (f : CauSeq ‚Ñö abs)` which converges to `Real.sqrt (mk f)` but do not prove (yet) that
this sequence actually converges to `Real.sqrt (mk f)`.

## Tags

square root
-/

set_option autoImplicit true

open Set Filter
open scoped Filter NNReal Topology

namespace NNReal

variable {x y : ‚Ñù‚â•0}

/-- Square root of a nonnegative real number. -/
-- porting note: was @[pp_nodot]
noncomputable def sqrt : ‚Ñù‚â•0 ‚âÉo ‚Ñù‚â•0 :=
  OrderIso.symm <| powOrderIso 2 two_ne_zero

theorem sqrt_le_sqrt_iff : sqrt x ‚â§ sqrt y ‚Üî x ‚â§ y :=
  sqrt.le_iff_le

theorem sqrt_lt_sqrt_iff : sqrt x < sqrt y ‚Üî x < y :=
  sqrt.lt_iff_lt

theorem sqrt_eq_iff_sq_eq : sqrt x = y ‚Üî y ^ 2 = x :=
  sqrt.toEquiv.apply_eq_iff_eq_symm_apply.trans eq_comm

theorem sqrt_le_iff : sqrt x ‚â§ y ‚Üî x ‚â§ y ^ 2 :=
  sqrt.to_galoisConnection _ _

theorem le_sqrt_iff : x ‚â§ sqrt y ‚Üî x ^ 2 ‚â§ y :=
  (sqrt.symm.to_galoisConnection _ _).symm

@[simp]
theorem sqrt_eq_zero : sqrt x = 0 ‚Üî x = 0 :=
  sqrt_eq_iff_sq_eq.trans <| by rw [eq_comm, sq, zero_mul]

@[simp]
theorem sqrt_zero : sqrt 0 = 0 :=
  sqrt_eq_zero.2 rfl

@[simp]
theorem sqrt_one : sqrt 1 = 1 :=
  sqrt_eq_iff_sq_eq.2 <| one_pow _

@[simp]
theorem sq_sqrt (x : ‚Ñù‚â•0) : sqrt x ^ 2 = x :=
  sqrt.symm_apply_apply x

@[simp]
theorem mul_self_sqrt (x : ‚Ñù‚â•0) : sqrt x * sqrt x = x := by rw [‚Üê sq, sq_sqrt]

@[simp]
theorem sqrt_sq (x : ‚Ñù‚â•0) : sqrt (x ^ 2) = x :=
  sqrt.apply_symm_apply x

@[simp]
theorem sqrt_mul_self (x : ‚Ñù‚â•0) : sqrt (x * x) = x := by rw [‚Üê sq, sqrt_sq x]

theorem sqrt_mul (x y : ‚Ñù‚â•0) : sqrt (x * y) = sqrt x * sqrt y := by
  rw [sqrt_eq_iff_sq_eq, mul_pow, sq_sqrt, sq_sqrt]

/-- `NNReal.sqrt` as a `MonoidWithZeroHom`. -/
noncomputable def sqrtHom : ‚Ñù‚â•0 ‚Üí*‚ÇÄ ‚Ñù‚â•0 :=
  ‚ü®‚ü®sqrt, sqrt_zero‚ü©, sqrt_one, sqrt_mul‚ü©

theorem sqrt_inv (x : ‚Ñù‚â•0) : sqrt x‚Åª¬π = (sqrt x)‚Åª¬π :=
  map_inv‚ÇÄ sqrtHom x

theorem sqrt_div (x y : ‚Ñù‚â•0) : sqrt (x / y) = sqrt x / sqrt y :=
  map_div‚ÇÄ sqrtHom x y

theorem continuous_sqrt : Continuous sqrt := sqrt.continuous

@[simp] theorem sqrt_pos : 0 < sqrt x ‚Üî 0 < x := by simp [pos_iff_ne_zero]

alias ‚ü®_, sqrt_pos_of_pos‚ü© := sqrt_pos

end NNReal

namespace Real

/-- An auxiliary sequence of rational numbers that converges to `Real.sqrt (mk f)`.
Currently this sequence is not used in `mathlib`.  -/
def sqrtAux (f : CauSeq ‚Ñö abs) : ‚Ñï ‚Üí ‚Ñö
  | 0 => mkRat (f 0).num.toNat.sqrt (f 0).den.sqrt
  | n + 1 =>
    let s := sqrtAux f n
    max 0 <| (s + f (n + 1) / s) / 2

theorem sqrtAux_nonneg (f : CauSeq ‚Ñö abs) : ‚àÄ i : ‚Ñï, 0 ‚â§ sqrtAux f i
  | 0 => by
    rw [sqrtAux, Rat.mkRat_eq, Rat.divInt_eq_div]; apply div_nonneg <;>
      exact Int.cast_nonneg.2 (Int.ofNat_nonneg _)
  | n + 1 => le_max_left _ _

/- TODO(Mario): finish the proof
theorem sqrt_aux_converges (f : cau_seq ‚Ñö abs) : ‚àÉ h x, 0 ‚â§ x ‚àß x * x = max 0 (mk f) ‚àß
    mk ‚ü®sqrt_aux f, h‚ü© = x :=
begin
  rcases sqrt_exists (le_max_left 0 (mk f)) with ‚ü®x, x0, hx‚ü©,
  suffices : ‚àÉ h, mk ‚ü®sqrt_aux f, h‚ü© = x,
  { exact this.imp (Œª h e, ‚ü®x, x0, hx, e‚ü©) },
  apply of_near,

  rsuffices ‚ü®Œ¥, Œ¥0, hŒ¥‚ü© : ‚àÉ Œ¥ > 0, ‚àÄ i, abs (‚Üë(sqrt_aux f i) - x) < Œ¥ / 2 ^ i,
  { intros }
end -/

-- porting note: todo: was @[pp_nodot]
/-- The square root of a real number. This returns 0 for negative inputs. -/
noncomputable def sqrt (x : ‚Ñù) : ‚Ñù :=
  NNReal.sqrt (Real.toNNReal x)

/-quotient.lift_on x
  (Œª f, mk ‚ü®sqrt_aux f, (sqrt_aux_converges f).fst‚ü©)
  (Œª f g e, begin
    rcases sqrt_aux_converges f with ‚ü®hf, x, x0, xf, xs‚ü©,
    rcases sqrt_aux_converges g with ‚ü®hg, y, y0, yg, ys‚ü©,
    refine xs.trans (eq.trans _ ys.symm),
    rw [‚Üê @mul_self_inj_of_nonneg ‚Ñù _ x y x0 y0, xf, yg],
    congr' 1, exact quotient.sound e
  end)-/
variable {x y : ‚Ñù}

@[simp, norm_cast]
theorem coe_sqrt {x : ‚Ñù‚â•0} : (NNReal.sqrt x : ‚Ñù) = Real.sqrt x := by
  rw [Real.sqrt, Real.toNNReal_coe]

@[continuity]
theorem continuous_sqrt : Continuous sqrt :=
  NNReal.continuous_coe.comp <| NNReal.continuous_sqrt.comp continuous_real_toNNReal

theorem sqrt_eq_zero_of_nonpos (h : x ‚â§ 0) : sqrt x = 0 := by simp [sqrt, Real.toNNReal_eq_zero.2 h]

theorem sqrt_nonneg (x : ‚Ñù) : 0 ‚â§ sqrt x :=
  NNReal.coe_nonneg _

@[simp]
theorem mul_self_sqrt (h : 0 ‚â§ x) : sqrt x * sqrt x = x := by
  rw [sqrt, ‚Üê NNReal.coe_mul, NNReal.mul_self_sqrt, Real.coe_toNNReal _ h]

@[simp]
theorem sqrt_mul_self (h : 0 ‚â§ x) : sqrt (x * x) = x :=
  (mul_self_inj_of_nonneg (sqrt_nonneg _) h).1 (mul_self_sqrt (mul_self_nonneg _))

theorem sqrt_eq_cases : sqrt x = y ‚Üî y * y = x ‚àß 0 ‚â§ y ‚à® x < 0 ‚àß y = 0 := by
  constructor
  ¬∑ rintro rfl
    cases' le_or_lt 0 x with hle hlt
    ¬∑ exact Or.inl ‚ü®mul_self_sqrt hle, sqrt_nonneg x‚ü©
    ¬∑ exact Or.inr ‚ü®hlt, sqrt_eq_zero_of_nonpos hlt.le‚ü©
  ¬∑ rintro (‚ü®rfl, hy‚ü© | ‚ü®hx, rfl‚ü©)
    exacts [sqrt_mul_self hy, sqrt_eq_zero_of_nonpos hx.le]

theorem sqrt_eq_iff_mul_self_eq (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) : sqrt x = y ‚Üî y * y = x :=
  ‚ü®fun h => by rw [‚Üê h, mul_self_sqrt hx], fun h => by rw [‚Üê h, sqrt_mul_self hy]‚ü©

theorem sqrt_eq_iff_mul_self_eq_of_pos (h : 0 < y) : sqrt x = y ‚Üî y * y = x := by
  simp [sqrt_eq_cases, h.ne', h.le]

@[simp]
theorem sqrt_eq_one : sqrt x = 1 ‚Üî x = 1 :=
  calc
    sqrt x = 1 ‚Üî 1 * 1 = x := sqrt_eq_iff_mul_self_eq_of_pos zero_lt_one
    _ ‚Üî x = 1 := by rw [eq_comm, mul_one]

@[simp]
theorem sq_sqrt (h : 0 ‚â§ x) : sqrt x ^ 2 = x := by rw [sq, mul_self_sqrt h]

@[simp]
theorem sqrt_sq (h : 0 ‚â§ x) : sqrt (x ^ 2) = x := by rw [sq, sqrt_mul_self h]

theorem sqrt_eq_iff_sq_eq (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) : sqrt x = y ‚Üî y ^ 2 = x := by
  rw [sq, sqrt_eq_iff_mul_self_eq hx hy]

theorem sqrt_mul_self_eq_abs (x : ‚Ñù) : sqrt (x * x) = |x| := by
  rw [‚Üê abs_mul_abs_self x, sqrt_mul_self (abs_nonneg _)]

theorem sqrt_sq_eq_abs (x : ‚Ñù) : sqrt (x ^ 2) = |x| := by rw [sq, sqrt_mul_self_eq_abs]

@[simp]
theorem sqrt_zero : sqrt 0 = 0 := by simp [sqrt]

@[simp]
theorem sqrt_one : sqrt 1 = 1 := by simp [sqrt]

@[simp]
theorem sqrt_le_sqrt_iff (hy : 0 ‚â§ y) : sqrt x ‚â§ sqrt y ‚Üî x ‚â§ y := by
  rw [sqrt, sqrt, NNReal.coe_le_coe, NNReal.sqrt_le_sqrt_iff, Real.toNNReal_le_toNNReal_iff hy]

@[simp]
theorem sqrt_lt_sqrt_iff (hx : 0 ‚â§ x) : sqrt x < sqrt y ‚Üî x < y :=
  lt_iff_lt_of_le_iff_le (sqrt_le_sqrt_iff hx)

theorem sqrt_lt_sqrt_iff_of_pos (hy : 0 < y) : sqrt x < sqrt y ‚Üî x < y := by
  rw [sqrt, sqrt, NNReal.coe_lt_coe, NNReal.sqrt_lt_sqrt_iff, toNNReal_lt_toNNReal_iff hy]

theorem sqrt_le_sqrt (h : x ‚â§ y) : sqrt x ‚â§ sqrt y := by
  rw [sqrt, sqrt, NNReal.coe_le_coe, NNReal.sqrt_le_sqrt_iff]
  exact toNNReal_le_toNNReal h

theorem sqrt_lt_sqrt (hx : 0 ‚â§ x) (h : x < y) : sqrt x < sqrt y :=
  (sqrt_lt_sqrt_iff hx).2 h

theorem sqrt_le_left (hy : 0 ‚â§ y) : sqrt x ‚â§ y ‚Üî x ‚â§ y ^ 2 := by
  rw [sqrt, ‚Üê Real.le_toNNReal_iff_coe_le hy, NNReal.sqrt_le_iff, sq, ‚Üê Real.toNNReal_mul hy,
    Real.toNNReal_le_toNNReal_iff (mul_self_nonneg y), sq]

theorem sqrt_le_iff : sqrt x ‚â§ y ‚Üî 0 ‚â§ y ‚àß x ‚â§ y ^ 2 := by
  rw [‚Üê and_iff_right_of_imp fun h => (sqrt_nonneg x).trans h, and_congr_right_iff]
  exact sqrt_le_left

theorem sqrt_lt (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) : sqrt x < y ‚Üî x < y ^ 2 := by
  rw [‚Üê sqrt_lt_sqrt_iff hx, sqrt_sq hy]

theorem sqrt_lt' (hy : 0 < y) : sqrt x < y ‚Üî x < y ^ 2 := by
  rw [‚Üê sqrt_lt_sqrt_iff_of_pos (pow_pos hy _), sqrt_sq hy.le]

/-- Note: if you want to conclude `x ‚â§ Real.sqrt y`, then use `Real.le_sqrt_of_sq_le`.  If you have
`x > 0`, consider using `Real.le_sqrt'` -/
theorem le_sqrt (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) : x ‚â§ sqrt y ‚Üî x ^ 2 ‚â§ y :=
  le_iff_le_iff_lt_iff_lt.2 <| sqrt_lt hy hx

theorem le_sqrt' (hx : 0 < x) : x ‚â§ sqrt y ‚Üî x ^ 2 ‚â§ y :=
  le_iff_le_iff_lt_iff_lt.2 <| sqrt_lt' hx

theorem abs_le_sqrt (h : x ^ 2 ‚â§ y) : |x| ‚â§ sqrt y := by
  rw [‚Üê sqrt_sq_eq_abs]; exact sqrt_le_sqrt h

theorem sq_le (h : 0 ‚â§ y) : x ^ 2 ‚â§ y ‚Üî -sqrt y ‚â§ x ‚àß x ‚â§ sqrt y := by
  constructor
  ¬∑ simpa only [abs_le] using abs_le_sqrt
  ¬∑ rw [‚Üê abs_le, ‚Üê sq_abs]
    exact (le_sqrt (abs_nonneg x) h).mp

theorem neg_sqrt_le_of_sq_le (h : x ^ 2 ‚â§ y) : -sqrt y ‚â§ x :=
  ((sq_le ((sq_nonneg x).trans h)).mp h).1

theorem le_sqrt_of_sq_le (h : x ^ 2 ‚â§ y) : x ‚â§ sqrt y :=
  ((sq_le ((sq_nonneg x).trans h)).mp h).2

@[simp]
theorem sqrt_inj (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) : sqrt x = sqrt y ‚Üî x = y := by
  simp [le_antisymm_iff, hx, hy]

@[simp]
theorem sqrt_eq_zero (h : 0 ‚â§ x) : sqrt x = 0 ‚Üî x = 0 := by simpa using sqrt_inj h le_rfl

theorem sqrt_eq_zero' : sqrt x = 0 ‚Üî x ‚â§ 0 := by
  rw [sqrt, NNReal.coe_eq_zero, NNReal.sqrt_eq_zero, Real.toNNReal_eq_zero]

theorem sqrt_ne_zero (h : 0 ‚â§ x) : sqrt x ‚â† 0 ‚Üî x ‚â† 0 := by rw [not_iff_not, sqrt_eq_zero h]

theorem sqrt_ne_zero' : sqrt x ‚â† 0 ‚Üî 0 < x := by rw [‚Üê not_le, not_iff_not, sqrt_eq_zero']

@[simp]
theorem sqrt_pos : 0 < sqrt x ‚Üî 0 < x :=
  lt_iff_lt_of_le_iff_le (Iff.trans (by simp [le_antisymm_iff, sqrt_nonneg]) sqrt_eq_zero')

alias ‚ü®_, sqrt_pos_of_pos‚ü© := sqrt_pos

end Real

namespace Mathlib.Meta.Positivity

open Lean Meta Qq Function

/-- Extension for the `positivity` tactic: a square root of a strictly positive nonnegative real is
positive. -/
@[positivity NNReal.sqrt _]
def evalNNRealSqrt : PositivityExt where eval {_ _} _zŒ± _pŒ± e := do
  let (.app _ (a : Q(NNReal))) ‚Üê whnfR e | throwError "not NNReal.sqrt"
  let zŒ±' ‚Üê synthInstanceQ (q(Zero NNReal) : Q(Type))
  let pŒ±' ‚Üê synthInstanceQ (q(PartialOrder NNReal) : Q(Type))
  let ra ‚Üê core zŒ±' pŒ±' a
  assertInstancesCommute
  match ra with
  | .positive pa => pure (.positive (q(NNReal.sqrt_pos_of_pos $pa) : Expr))
  | _ => failure -- this case is dealt with by generic nonnegativity of nnreals

/-- Extension for the `positivity` tactic: a square root is nonnegative, and is strictly positive if
its input is. -/
@[positivity Real.sqrt _]
def evalSqrt : PositivityExt where eval {_ _} _zŒ± _pŒ± e := do
  let (.app _ (a : Q(Real))) ‚Üê whnfR e | throwError "not Real.sqrt"
  let zŒ±' ‚Üê synthInstanceQ (q(Zero Real) : Q(Type))
  let pŒ±' ‚Üê synthInstanceQ (q(PartialOrder Real) : Q(Type))
  let ra ‚Üê catchNone <| core zŒ±' pŒ±' a
  assertInstancesCommute
  match ra with
  | .positive pa => pure (.positive (q(Real.sqrt_pos_of_pos $pa) : Expr))
  | _ => pure (.nonnegative (q(Real.sqrt_nonneg $a) : Expr))

end Mathlib.Meta.Positivity

namespace Real

@[simp]
theorem sqrt_mul (hx : 0 ‚â§ x) (y : ‚Ñù) : sqrt (x * y) = sqrt x * sqrt y := by
  simp_rw [sqrt, ‚Üê NNReal.coe_mul, NNReal.coe_eq, Real.toNNReal_mul hx, NNReal.sqrt_mul]

@[simp]
theorem sqrt_mul' (x) {y : ‚Ñù} (hy : 0 ‚â§ y) : sqrt (x * y) = sqrt x * sqrt y := by
  rw [mul_comm, sqrt_mul hy, mul_comm]

@[simp]
theorem sqrt_inv (x : ‚Ñù) : sqrt x‚Åª¬π = (sqrt x)‚Åª¬π := by
  rw [sqrt, Real.toNNReal_inv, NNReal.sqrt_inv, NNReal.coe_inv, sqrt]

@[simp]
theorem sqrt_div (hx : 0 ‚â§ x) (y : ‚Ñù) : sqrt (x / y) = sqrt x / sqrt y := by
  rw [division_def, sqrt_mul hx, sqrt_inv, division_def]

@[simp]
theorem sqrt_div' (x) {y : ‚Ñù} (hy : 0 ‚â§ y) : sqrt (x / y) = sqrt x / sqrt y := by
  rw [division_def, sqrt_mul' x (inv_nonneg.2 hy), sqrt_inv, division_def]

@[simp]
theorem div_sqrt : x / sqrt x = sqrt x := by
  cases' le_or_lt x 0 with h h
  ¬∑ rw [sqrt_eq_zero'.mpr h, div_zero]
  ¬∑ rw [div_eq_iff (sqrt_ne_zero'.mpr h), mul_self_sqrt h.le]

theorem sqrt_div_self' : sqrt x / x = 1 / sqrt x := by rw [‚Üê div_sqrt, one_div_div, div_sqrt]

theorem sqrt_div_self : sqrt x / x = (sqrt x)‚Åª¬π := by rw [sqrt_div_self', one_div]

theorem lt_sqrt (hx : 0 ‚â§ x) : x < sqrt y ‚Üî x ^ 2 < y := by
  rw [‚Üê sqrt_lt_sqrt_iff (sq_nonneg _), sqrt_sq hx]

theorem sq_lt : x ^ 2 < y ‚Üî -sqrt y < x ‚àß x < sqrt y := by
  rw [‚Üê abs_lt, ‚Üê sq_abs, lt_sqrt (abs_nonneg _)]

theorem neg_sqrt_lt_of_sq_lt (h : x ^ 2 < y) : -sqrt y < x :=
  (sq_lt.mp h).1

theorem lt_sqrt_of_sq_lt (h : x ^ 2 < y) : x < sqrt y :=
  (sq_lt.mp h).2

theorem lt_sq_of_sqrt_lt {x y : ‚Ñù} (h : sqrt x < y) : x < y ^ 2 := by
  have hy := x.sqrt_nonneg.trans_lt h
  rwa [‚Üê sqrt_lt_sqrt_iff_of_pos (sq_pos_of_pos hy), sqrt_sq hy.le]

/-- The natural square root is at most the real square root -/
theorem nat_sqrt_le_real_sqrt {a : ‚Ñï} : ‚Üë(Nat.sqrt a) ‚â§ Real.sqrt ‚Üëa := by
  rw [Real.le_sqrt (Nat.cast_nonneg _) (Nat.cast_nonneg _)]
  norm_cast
  exact Nat.sqrt_le' a

/-- The real square root is at most the natural square root plus one -/
theorem real_sqrt_le_nat_sqrt_succ {a : ‚Ñï} : Real.sqrt ‚Üëa ‚â§ Nat.sqrt a + 1 := by
  rw [Real.sqrt_le_iff]
  constructor
  ¬∑ norm_cast
    apply zero_le
  ¬∑ norm_cast
    exact le_of_lt (Nat.lt_succ_sqrt' a)

/-- Although the instance `IsROrC.toStarOrderedRing` exists, it is locked behind the
`ComplexOrder` scope because currently the order on `‚ÑÇ` is not enabled globally. But we
want `StarOrderedRing ‚Ñù` to be available globally, so we include this instance separately.
In addition, providing this instance here makes it available earlier in the import
hierarchy; otherwise in order to access it we would need to import `Data.IsROrC.Basic` -/
instance : StarOrderedRing ‚Ñù :=
  StarOrderedRing.ofNonnegIff' add_le_add_left fun r => by
    refine ‚ü®fun hr => ‚ü®sqrt r, (mul_self_sqrt hr).symm‚ü©, ?_‚ü©
    rintro ‚ü®s, rfl‚ü©
    exact mul_self_nonneg s

end Real

open Real

variable {Œ± : Type*}

theorem Filter.Tendsto.sqrt {f : Œ± ‚Üí ‚Ñù} {l : Filter Œ±} {x : ‚Ñù} (h : Tendsto f l (ùìù x)) :
    Tendsto (fun x => sqrt (f x)) l (ùìù (sqrt x)) :=
  (continuous_sqrt.tendsto _).comp h

variable [TopologicalSpace Œ±] {f : Œ± ‚Üí ‚Ñù} {s : Set Œ±} {x : Œ±}

nonrec theorem ContinuousWithinAt.sqrt (h : ContinuousWithinAt f s x) :
    ContinuousWithinAt (fun x => sqrt (f x)) s x :=
  h.sqrt

nonrec theorem ContinuousAt.sqrt (h : ContinuousAt f x) : ContinuousAt (fun x => sqrt (f x)) x :=
  h.sqrt

theorem ContinuousOn.sqrt (h : ContinuousOn f s) : ContinuousOn (fun x => sqrt (f x)) s :=
  fun x hx => (h x hx).sqrt

@[continuity]
theorem Continuous.sqrt (h : Continuous f) : Continuous fun x => sqrt (f x) :=
  continuous_sqrt.comp h
