/-
Copyright (c) 2020 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Floris van Doorn, Yury Kudryashov
-/
import Mathlib.Topology.Instances.NNReal.Lemmas
import Mathlib.Topology.Order.MonotoneContinuity

/-!
# Square root of a real number

In this file we define

* `NNReal.sqrt` to be the square root of a nonnegative real number.
* `Real.sqrt` to be the square root of a real number, defined to be zero on negative numbers.

Then we prove some basic properties of these functions.

## Implementation notes

We define `NNReal.sqrt` as the noncomputable inverse to the function `x ‚Ü¶ x * x`. We use general
theory of inverses of strictly monotone functions to prove that `NNReal.sqrt x` exists. As a side
effect, `NNReal.sqrt` is a bundled `OrderIso`, so for `NNReal` numbers we get continuity as well as
theorems like `NNReal.sqrt x ‚â§ y ‚Üî x ‚â§ y * y` for free.

Then we define `Real.sqrt x` to be `NNReal.sqrt (Real.toNNReal x)`.

## Tags

square root
-/

open Set Filter
open scoped Filter NNReal Topology

namespace NNReal

variable {x y : ‚Ñù‚â•0}

/-- Square root of a nonnegative real number. -/
@[pp_nodot]
noncomputable def sqrt : ‚Ñù‚â•0 ‚âÉo ‚Ñù‚â•0 :=
  OrderIso.symm <| powOrderIso 2 two_ne_zero

@[simp] lemma sq_sqrt (x : ‚Ñù‚â•0) : sqrt x ^ 2 = x := sqrt.symm_apply_apply _

@[simp] lemma sqrt_sq (x : ‚Ñù‚â•0) : sqrt (x ^ 2) = x := sqrt.apply_symm_apply _

@[simp] lemma mul_self_sqrt (x : ‚Ñù‚â•0) : sqrt x * sqrt x = x := by rw [‚Üê sq, sq_sqrt]

@[simp] lemma sqrt_mul_self (x : ‚Ñù‚â•0) : sqrt (x * x) = x := by rw [‚Üê sq, sqrt_sq]

lemma sqrt_le_sqrt : sqrt x ‚â§ sqrt y ‚Üî x ‚â§ y := sqrt.le_iff_le

lemma sqrt_lt_sqrt : sqrt x < sqrt y ‚Üî x < y := sqrt.lt_iff_lt

lemma sqrt_eq_iff_eq_sq : sqrt x = y ‚Üî x = y ^ 2 := sqrt.toEquiv.apply_eq_iff_eq_symm_apply

lemma sqrt_le_iff_le_sq : sqrt x ‚â§ y ‚Üî x ‚â§ y ^ 2 := sqrt.to_galoisConnection _ _

lemma le_sqrt_iff_sq_le : x ‚â§ sqrt y ‚Üî x ^ 2 ‚â§ y := (sqrt.symm.to_galoisConnection _ _).symm

@[simp] lemma sqrt_eq_zero : sqrt x = 0 ‚Üî x = 0 := by simp [sqrt_eq_iff_eq_sq]

@[simp] lemma sqrt_eq_one : sqrt x = 1 ‚Üî x = 1 := by simp [sqrt_eq_iff_eq_sq]

@[simp] lemma sqrt_zero : sqrt 0 = 0 := by simp

@[simp] lemma sqrt_one : sqrt 1 = 1 := by simp

@[simp] lemma sqrt_le_one : sqrt x ‚â§ 1 ‚Üî x ‚â§ 1 := by rw [‚Üê sqrt_one, sqrt_le_sqrt, sqrt_one]
@[simp] lemma one_le_sqrt : 1 ‚â§ sqrt x ‚Üî 1 ‚â§ x := by rw [‚Üê sqrt_one, sqrt_le_sqrt, sqrt_one]

theorem sqrt_mul (x y : ‚Ñù‚â•0) : sqrt (x * y) = sqrt x * sqrt y := by
  rw [sqrt_eq_iff_eq_sq, mul_pow, sq_sqrt, sq_sqrt]

/-- `NNReal.sqrt` as a `MonoidWithZeroHom`. -/
noncomputable def sqrtHom : ‚Ñù‚â•0 ‚Üí*‚ÇÄ ‚Ñù‚â•0 :=
  ‚ü®‚ü®sqrt, sqrt_zero‚ü©, sqrt_one, sqrt_mul‚ü©

theorem sqrt_inv (x : ‚Ñù‚â•0) : sqrt x‚Åª¬π = (sqrt x)‚Åª¬π :=
  map_inv‚ÇÄ sqrtHom x

theorem sqrt_div (x y : ‚Ñù‚â•0) : sqrt (x / y) = sqrt x / sqrt y :=
  map_div‚ÇÄ sqrtHom x y

@[continuity, fun_prop]
theorem continuous_sqrt : Continuous sqrt := sqrt.continuous

@[simp] theorem sqrt_pos : 0 < sqrt x ‚Üî 0 < x := by simp [pos_iff_ne_zero]

alias ‚ü®_, sqrt_pos_of_pos‚ü© := sqrt_pos

attribute [bound] sqrt_pos_of_pos

@[simp] theorem isSquare (x : ‚Ñù‚â•0) : IsSquare x := ‚ü®_, mul_self_sqrt _ |>.symm‚ü©

end NNReal

namespace Real

/-- The square root of a real number. This returns 0 for negative inputs.

This has notation `‚àöx`. Note that `‚àöx‚Åª¬π` is parsed as `‚àö(x‚Åª¬π)`. -/
@[irreducible] noncomputable def sqrt (x : ‚Ñù) : ‚Ñù :=
  NNReal.sqrt (Real.toNNReal x)

-- TODO: replace this with a typeclass
@[inherit_doc]
prefix:max "‚àö" => Real.sqrt

variable {x y : ‚Ñù}

@[simp, norm_cast]
theorem coe_sqrt {x : ‚Ñù‚â•0} : (NNReal.sqrt x : ‚Ñù) = ‚àö(x : ‚Ñù) := by
  rw [Real.sqrt, Real.toNNReal_coe]

@[continuity]
theorem continuous_sqrt : Continuous (‚àö¬∑ : ‚Ñù ‚Üí ‚Ñù) := by
  simp only [sqrt]
  exact NNReal.continuous_coe.comp <| NNReal.continuous_sqrt.comp continuous_real_toNNReal

theorem sqrt_eq_zero_of_nonpos (h : x ‚â§ 0) : sqrt x = 0 := by simp [sqrt, Real.toNNReal_eq_zero.2 h]

@[simp] theorem sqrt_nonneg (x : ‚Ñù) : 0 ‚â§ ‚àöx := by
  simp only [sqrt]
  exact NNReal.coe_nonneg _

@[simp]
theorem mul_self_sqrt (h : 0 ‚â§ x) : ‚àöx * ‚àöx = x := by
  rw [Real.sqrt, ‚Üê NNReal.coe_mul, NNReal.mul_self_sqrt, Real.coe_toNNReal _ h]

@[simp]
theorem sqrt_mul_self (h : 0 ‚â§ x) : ‚àö(x * x) = x :=
  (mul_self_inj_of_nonneg (sqrt_nonneg _) h).1 (mul_self_sqrt (mul_self_nonneg _))

theorem sqrt_eq_cases : ‚àöx = y ‚Üî y * y = x ‚àß 0 ‚â§ y ‚à® x < 0 ‚àß y = 0 := by
  constructor
  ¬∑ rintro rfl
    rcases le_or_gt 0 x with hle | hlt
    ¬∑ exact Or.inl ‚ü®mul_self_sqrt hle, sqrt_nonneg x‚ü©
    ¬∑ exact Or.inr ‚ü®hlt, sqrt_eq_zero_of_nonpos hlt.le‚ü©
  ¬∑ rintro (‚ü®rfl, hy‚ü© | ‚ü®hx, rfl‚ü©)
    exacts [sqrt_mul_self hy, sqrt_eq_zero_of_nonpos hx.le]

theorem sqrt_eq_iff_mul_self_eq (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) : ‚àöx = y ‚Üî x = y * y :=
  ‚ü®fun h => by rw [‚Üê h, mul_self_sqrt hx], fun h => by rw [h, sqrt_mul_self hy]‚ü©

theorem sqrt_eq_iff_mul_self_eq_of_pos (h : 0 < y) : ‚àöx = y ‚Üî y * y = x := by
  simp [sqrt_eq_cases, h.ne', h.le]

@[simp]
theorem sqrt_eq_one : ‚àöx = 1 ‚Üî x = 1 :=
  calc
    ‚àöx = 1 ‚Üî 1 * 1 = x := sqrt_eq_iff_mul_self_eq_of_pos zero_lt_one
    _ ‚Üî x = 1 := by rw [eq_comm, mul_one]

@[simp]
theorem sq_sqrt (h : 0 ‚â§ x) : ‚àöx ^ 2 = x := by rw [sq, mul_self_sqrt h]

@[simp]
theorem sqrt_sq (h : 0 ‚â§ x) : ‚àö(x ^ 2) = x := by rw [sq, sqrt_mul_self h]

theorem sqrt_eq_iff_eq_sq (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) : ‚àöx = y ‚Üî x = y ^ 2 := by
  rw [sq, sqrt_eq_iff_mul_self_eq hx hy]

theorem sqrt_mul_self_eq_abs (x : ‚Ñù) : ‚àö(x * x) = |x| := by
  rw [‚Üê abs_mul_abs_self x, sqrt_mul_self (abs_nonneg _)]

theorem sqrt_sq_eq_abs (x : ‚Ñù) : ‚àö(x ^ 2) = |x| := by rw [sq, sqrt_mul_self_eq_abs]

@[simp, grind =]
theorem sqrt_zero : ‚àö0 = 0 := by simp [Real.sqrt]

@[simp, grind =]
theorem sqrt_one : ‚àö1 = 1 := by simp [Real.sqrt]

@[simp]
theorem sqrt_le_sqrt_iff (hy : 0 ‚â§ y) : ‚àöx ‚â§ ‚àöy ‚Üî x ‚â§ y := by
  rw [Real.sqrt, Real.sqrt, NNReal.coe_le_coe, NNReal.sqrt_le_sqrt, toNNReal_le_toNNReal_iff hy]

@[simp]
theorem sqrt_lt_sqrt_iff (hx : 0 ‚â§ x) : ‚àöx < ‚àöy ‚Üî x < y :=
  lt_iff_lt_of_le_iff_le (sqrt_le_sqrt_iff hx)

theorem sqrt_lt_sqrt_iff_of_pos (hy : 0 < y) : ‚àöx < ‚àöy ‚Üî x < y := by
  rw [Real.sqrt, Real.sqrt, NNReal.coe_lt_coe, NNReal.sqrt_lt_sqrt, toNNReal_lt_toNNReal_iff hy]

@[bound]
theorem sqrt_le_sqrt (h : x ‚â§ y) : ‚àöx ‚â§ ‚àöy := by
  rw [Real.sqrt, Real.sqrt, NNReal.coe_le_coe, NNReal.sqrt_le_sqrt]
  exact toNNReal_le_toNNReal h

@[gcongr]
theorem sqrt_monotone : Monotone Real.sqrt :=
  fun _ _ ‚Ü¶ sqrt_le_sqrt

@[gcongr, bound]
theorem sqrt_lt_sqrt (hx : 0 ‚â§ x) (h : x < y) : ‚àöx < ‚àöy :=
  (sqrt_lt_sqrt_iff hx).2 h

theorem sqrt_le_left (hy : 0 ‚â§ y) : ‚àöx ‚â§ y ‚Üî x ‚â§ y ^ 2 := by
  rw [sqrt, ‚Üê Real.le_toNNReal_iff_coe_le hy, NNReal.sqrt_le_iff_le_sq, sq, ‚Üê Real.toNNReal_mul hy,
    Real.toNNReal_le_toNNReal_iff (mul_self_nonneg y), sq]

theorem sqrt_le_iff : ‚àöx ‚â§ y ‚Üî 0 ‚â§ y ‚àß x ‚â§ y ^ 2 := by
  rw [‚Üê and_iff_right_of_imp fun h => (sqrt_nonneg x).trans h, and_congr_right_iff]
  exact sqrt_le_left

theorem sqrt_lt (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) : ‚àöx < y ‚Üî x < y ^ 2 := by
  rw [‚Üê sqrt_lt_sqrt_iff hx, sqrt_sq hy]

theorem sqrt_lt' (hy : 0 < y) : ‚àöx < y ‚Üî x < y ^ 2 := by
  rw [‚Üê sqrt_lt_sqrt_iff_of_pos (pow_pos hy _), sqrt_sq hy.le]

/-- Note: if you want to conclude `x ‚â§ ‚àöy`, then use `Real.le_sqrt_of_sq_le`.
If you have `x > 0`, consider using `Real.le_sqrt'` -/
theorem le_sqrt (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) : x ‚â§ ‚àöy ‚Üî x ^ 2 ‚â§ y :=
  le_iff_le_iff_lt_iff_lt.2 <| sqrt_lt hy hx

theorem le_sqrt' (hx : 0 < x) : x ‚â§ ‚àöy ‚Üî x ^ 2 ‚â§ y :=
  le_iff_le_iff_lt_iff_lt.2 <| sqrt_lt' hx

theorem abs_le_sqrt (h : x ^ 2 ‚â§ y) : |x| ‚â§ ‚àöy := by
  rw [‚Üê sqrt_sq_eq_abs]; exact sqrt_le_sqrt h

theorem sq_le (h : 0 ‚â§ y) : x ^ 2 ‚â§ y ‚Üî -‚àöy ‚â§ x ‚àß x ‚â§ ‚àöy := by
  constructor
  ¬∑ simpa only [abs_le] using abs_le_sqrt
  ¬∑ rw [‚Üê abs_le, ‚Üê sq_abs]
    exact (le_sqrt (abs_nonneg x) h).mp

theorem neg_sqrt_le_of_sq_le (h : x ^ 2 ‚â§ y) : -‚àöy ‚â§ x :=
  ((sq_le ((sq_nonneg x).trans h)).mp h).1

theorem le_sqrt_of_sq_le (h : x ^ 2 ‚â§ y) : x ‚â§ ‚àöy :=
  ((sq_le ((sq_nonneg x).trans h)).mp h).2

@[simp]
theorem sqrt_inj (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) : ‚àöx = ‚àöy ‚Üî x = y := by
  simp [le_antisymm_iff, hx, hy]

@[simp]
theorem sqrt_eq_zero (h : 0 ‚â§ x) : ‚àöx = 0 ‚Üî x = 0 := by simpa using sqrt_inj h le_rfl

theorem sqrt_eq_zero' : ‚àöx = 0 ‚Üî x ‚â§ 0 := by
  rw [sqrt, NNReal.coe_eq_zero, NNReal.sqrt_eq_zero, Real.toNNReal_eq_zero]

theorem sqrt_ne_zero (h : 0 ‚â§ x) : ‚àöx ‚â† 0 ‚Üî x ‚â† 0 := by rw [not_iff_not, sqrt_eq_zero h]

theorem sqrt_ne_zero' : ‚àöx ‚â† 0 ‚Üî 0 < x := by rw [‚Üê not_le, not_iff_not, sqrt_eq_zero']

/-- Variant of `sq_sqrt` without a non-negativity assumption on `x`. -/
theorem sq_sqrt' : ‚àöx ^ 2 = max x 0 := by
  rcases lt_trichotomy x 0 with _ | _ | _ <;> grind [sqrt_eq_zero', sq_sqrt]

-- Add the rule for `‚àöx ^ 2` to the grind whiteboard whenever we see a real square root.
grind_pattern sq_sqrt' => ‚àöx

-- Check that `grind` can discharge non-zero goals for square roots of positive numerals.
example : ‚àö7 ‚â† 0 := by grind

@[simp]
theorem sqrt_pos : 0 < ‚àöx ‚Üî 0 < x :=
  lt_iff_lt_of_le_iff_le (Iff.trans (by simp [le_antisymm_iff, sqrt_nonneg]) sqrt_eq_zero')

alias ‚ü®_, sqrt_pos_of_pos‚ü© := sqrt_pos

lemma sqrt_le_sqrt_iff' (hx : 0 < x) : ‚àöx ‚â§ ‚àöy ‚Üî x ‚â§ y := by
  obtain hy | hy := le_total y 0
  ¬∑ exact iff_of_false ((sqrt_eq_zero_of_nonpos hy).trans_lt <| sqrt_pos.2 hx).not_ge
      (hy.trans_lt hx).not_ge
  ¬∑ exact sqrt_le_sqrt_iff hy

@[simp] lemma one_le_sqrt : 1 ‚â§ ‚àöx ‚Üî 1 ‚â§ x := by
  rw [‚Üê sqrt_one, sqrt_le_sqrt_iff' zero_lt_one, sqrt_one]

@[simp] lemma sqrt_le_one : ‚àöx ‚â§ 1 ‚Üî x ‚â§ 1 := by
  rw [‚Üê sqrt_one, sqrt_le_sqrt_iff zero_le_one, sqrt_one]

@[simp] lemma isSquare_iff : IsSquare x ‚Üî 0 ‚â§ x :=
  ‚ü®(¬∑.nonneg), (‚ü®‚àöx, mul_self_sqrt ¬∑ |>.symm‚ü©)‚ü©

end Real

namespace Mathlib.Meta.Positivity

open Lean Meta Qq Function

/-- Extension for the `positivity` tactic: a square root of a strictly positive nonnegative real is
positive. -/
@[positivity NNReal.sqrt _]
def evalNNRealSqrt : PositivityExt where eval {u Œ±} _zŒ± _pŒ± e := do
  match u, Œ±, e with
  | 0, ~q(NNReal), ~q(NNReal.sqrt $a) =>
    let ra ‚Üê core  q(inferInstance) q(inferInstance) a
    assertInstancesCommute
    match ra with
    | .positive pa => pure (.positive q(NNReal.sqrt_pos_of_pos $pa))
    | _ => failure -- this case is dealt with by generic nonnegativity of nnreals
  | _, _, _ => throwError "not NNReal.sqrt"

/-- Extension for the `positivity` tactic: a square root is nonnegative, and is strictly positive if
its input is. -/
@[positivity ‚àö_]
def evalSqrt : PositivityExt where eval {u Œ±} _zŒ± _pŒ± e := do
  match u, Œ±, e with
  | 0, ~q(‚Ñù), ~q(‚àö$a) =>
    let ra ‚Üê catchNone <| core q(inferInstance) q(inferInstance) a
    assertInstancesCommute
    match ra with
    | .positive pa => pure (.positive q(Real.sqrt_pos_of_pos $pa))
    | _ => pure (.nonnegative q(Real.sqrt_nonneg $a))
  | _, _, _ => throwError "not Real.sqrt"

end Mathlib.Meta.Positivity

namespace Real

lemma one_lt_sqrt_two : 1 < ‚àö2 := by rw [‚Üê Real.sqrt_one]; gcongr; simp

lemma sqrt_two_lt_three_halves : ‚àö2 < 3 / 2 := by
  suffices 2 * ‚àö2 < 3 by linarith
  rw [‚Üê sq_lt_sq‚ÇÄ (by positivity) (by positivity), mul_pow, Real.sq_sqrt (by positivity)]
  norm_num

lemma inv_sqrt_two_sub_one : (‚àö2 - 1)‚Åª¬π = ‚àö2 + 1 := by
  grind

@[simp]
theorem sqrt_mul {x : ‚Ñù} (hx : 0 ‚â§ x) (y : ‚Ñù) : ‚àö(x * y) = ‚àöx * ‚àöy := by
  simp_rw [Real.sqrt, ‚Üê NNReal.coe_mul, NNReal.coe_inj, Real.toNNReal_mul hx, NNReal.sqrt_mul]

@[simp]
theorem sqrt_mul' (x) {y : ‚Ñù} (hy : 0 ‚â§ y) : ‚àö(x * y) = ‚àöx * ‚àöy := by
  rw [mul_comm, sqrt_mul hy, mul_comm]

@[simp]
theorem sqrt_inv (x : ‚Ñù) : ‚àöx‚Åª¬π = (‚àöx)‚Åª¬π := by
  rw [Real.sqrt, Real.toNNReal_inv, NNReal.sqrt_inv, NNReal.coe_inv, Real.sqrt]

@[simp]
theorem sqrt_div {x : ‚Ñù} (hx : 0 ‚â§ x) (y : ‚Ñù) : ‚àö(x / y) = ‚àöx / ‚àöy := by
  rw [division_def, sqrt_mul hx, sqrt_inv, division_def]

@[simp]
theorem sqrt_div' (x) {y : ‚Ñù} (hy : 0 ‚â§ y) : ‚àö(x / y) = ‚àöx / ‚àöy := by
  rw [division_def, sqrt_mul' x (inv_nonneg.2 hy), sqrt_inv, division_def]

variable {x y : ‚Ñù}

@[simp]
theorem div_sqrt : x / ‚àöx = ‚àöx := by
  grind

theorem sqrt_div_self' : ‚àöx / x = 1 / ‚àöx := by rw [‚Üê div_sqrt, one_div_div, div_sqrt]

theorem sqrt_div_self : ‚àöx / x = (‚àöx)‚Åª¬π := by rw [sqrt_div_self', one_div]

theorem lt_sqrt (hx : 0 ‚â§ x) : x < ‚àöy ‚Üî x ^ 2 < y := by
  rw [‚Üê sqrt_lt_sqrt_iff (sq_nonneg _), sqrt_sq hx]

theorem sq_lt : x ^ 2 < y ‚Üî -‚àöy < x ‚àß x < ‚àöy := by
  rw [‚Üê abs_lt, ‚Üê sq_abs, lt_sqrt (abs_nonneg _)]

theorem neg_sqrt_lt_of_sq_lt (h : x ^ 2 < y) : -‚àöy < x :=
  (sq_lt.mp h).1

theorem lt_sqrt_of_sq_lt (h : x ^ 2 < y) : x < ‚àöy :=
  (sq_lt.mp h).2

theorem lt_sq_of_sqrt_lt (h : ‚àöx < y) : x < y ^ 2 := by
  have hy := x.sqrt_nonneg.trans_lt h
  rwa [‚Üê sqrt_lt_sqrt_iff_of_pos (sq_pos_of_pos hy), sqrt_sq hy.le]

/-- The natural square root is at most the real square root -/
theorem nat_sqrt_le_real_sqrt {a : ‚Ñï} : ‚Üë(Nat.sqrt a) ‚â§ ‚àö(a : ‚Ñù) := by
  rw [Real.le_sqrt (Nat.cast_nonneg _) (Nat.cast_nonneg _)]
  norm_cast
  exact Nat.sqrt_le' a

/-- The real square root is less than the natural square root plus one -/
theorem real_sqrt_lt_nat_sqrt_succ {a : ‚Ñï} : ‚àö(a : ‚Ñù) < Nat.sqrt a + 1 := by
  rw [sqrt_lt (by simp)] <;> norm_cast
  ¬∑ exact Nat.lt_succ_sqrt' a
  ¬∑ exact Nat.le_add_left 0 (Nat.sqrt a + 1)

/-- The real square root is at most the natural square root plus one -/
theorem real_sqrt_le_nat_sqrt_succ {a : ‚Ñï} : ‚àö(a : ‚Ñù) ‚â§ Nat.sqrt a + 1 :=
  real_sqrt_lt_nat_sqrt_succ.le

/-- The floor of the real square root is the same as the natural square root. -/
@[simp]
theorem floor_real_sqrt_eq_nat_sqrt {a : ‚Ñï} : ‚åä‚àö(a : ‚Ñù)‚åã = Nat.sqrt a := by
  rw [Int.floor_eq_iff]
  exact ‚ü®nat_sqrt_le_real_sqrt, real_sqrt_lt_nat_sqrt_succ‚ü©

/-- The natural floor of the real square root is the same as the natural square root. -/
@[simp]
theorem nat_floor_real_sqrt_eq_nat_sqrt {a : ‚Ñï} : ‚åä‚àö(a : ‚Ñù)‚åã‚Çä = Nat.sqrt a := by
  rw [Nat.floor_eq_iff (sqrt_nonneg a)]
  exact ‚ü®nat_sqrt_le_real_sqrt, real_sqrt_lt_nat_sqrt_succ‚ü©

/-- Bernoulli's inequality for exponent `1 / 2`, stated using `sqrt`. -/
theorem sqrt_one_add_le (h : -1 ‚â§ x) : ‚àö(1 + x) ‚â§ 1 + x / 2 := by
  refine sqrt_le_iff.mpr ‚ü®by linarith, ?_‚ü©
  calc 1 + x
    _ ‚â§ 1 + x + (x / 2) ^ 2 := le_add_of_nonneg_right <| sq_nonneg _
    _ = _ := by ring

end Real

open Real

variable {Œ± : Type*}

theorem Filter.Tendsto.sqrt {f : Œ± ‚Üí ‚Ñù} {l : Filter Œ±} {x : ‚Ñù} (h : Tendsto f l (ùìù x)) :
    Tendsto (fun x => ‚àö(f x)) l (ùìù (‚àöx)) :=
  (continuous_sqrt.tendsto _).comp h

variable [TopologicalSpace Œ±] {f : Œ± ‚Üí ‚Ñù} {s : Set Œ±} {x : Œ±}

nonrec theorem ContinuousWithinAt.sqrt (h : ContinuousWithinAt f s x) :
    ContinuousWithinAt (fun x => ‚àö(f x)) s x :=
  h.sqrt

@[fun_prop]
nonrec theorem ContinuousAt.sqrt (h : ContinuousAt f x) : ContinuousAt (fun x => ‚àö(f x)) x :=
  h.sqrt

@[fun_prop]
theorem ContinuousOn.sqrt (h : ContinuousOn f s) : ContinuousOn (fun x => ‚àö(f x)) s :=
  fun x hx => (h x hx).sqrt

@[continuity, fun_prop]
theorem Continuous.sqrt (h : Continuous f) : Continuous fun x => ‚àö(f x) :=
  continuous_sqrt.comp h

namespace NNReal
variable {Œπ : Type*}
open Finset

/-- **Cauchy-Schwarz inequality** for finsets using square roots in `‚Ñù‚â•0`. -/
lemma sum_mul_le_sqrt_mul_sqrt (s : Finset Œπ) (f g : Œπ ‚Üí ‚Ñù‚â•0) :
    ‚àë i ‚àà s, f i * g i ‚â§ sqrt (‚àë i ‚àà s, f i ^ 2) * sqrt (‚àë i ‚àà s, g i ^ 2) :=
  (le_sqrt_iff_sq_le.2 <| sum_mul_sq_le_sq_mul_sq _ _ _).trans_eq <| sqrt_mul _ _

/-- **Cauchy-Schwarz inequality** for finsets using square roots in `‚Ñù‚â•0`. -/
lemma sum_sqrt_mul_sqrt_le (s : Finset Œπ) (f g : Œπ ‚Üí ‚Ñù‚â•0) :
    ‚àë i ‚àà s, sqrt (f i) * sqrt (g i) ‚â§ sqrt (‚àë i ‚àà s, f i) * sqrt (‚àë i ‚àà s, g i) := by
  simpa [*] using sum_mul_le_sqrt_mul_sqrt _ (fun x ‚Ü¶ sqrt (f x)) (fun x ‚Ü¶ sqrt (g x))

end NNReal

namespace Real
variable {Œπ : Type*} {f g : Œπ ‚Üí ‚Ñù}
open Finset

/-- **Cauchy-Schwarz inequality** for finsets using square roots in `‚Ñù`. -/
lemma sum_mul_le_sqrt_mul_sqrt (s : Finset Œπ) (f g : Œπ ‚Üí ‚Ñù) :
    ‚àë i ‚àà s, f i * g i ‚â§ ‚àö(‚àë i ‚àà s, f i ^ 2) * ‚àö(‚àë i ‚àà s, g i ^ 2) :=
  (le_sqrt_of_sq_le <| sum_mul_sq_le_sq_mul_sq _ _ _).trans_eq <| sqrt_mul
    (sum_nonneg fun _ _ ‚Ü¶ by positivity) _

/-- **Cauchy-Schwarz inequality** for finsets using square roots in `‚Ñù`. -/
lemma sum_sqrt_mul_sqrt_le (s : Finset Œπ) (hf : ‚àÄ i, 0 ‚â§ f i) (hg : ‚àÄ i, 0 ‚â§ g i) :
    ‚àë i ‚àà s, ‚àö(f i) * ‚àö(g i) ‚â§ ‚àö(‚àë i ‚àà s, f i) * ‚àö(‚àë i ‚àà s, g i) := by
  simpa [*] using sum_mul_le_sqrt_mul_sqrt _ (fun x ‚Ü¶ ‚àö(f x)) (fun x ‚Ü¶ ‚àö(g x))

end Real
