/-
Copyright (c) 2021 Hanting Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Hanting Zhang
-/
import Mathlib.Analysis.SpecialFunctions.Integrals.Basic

/-! # The Wallis formula for Pi

This file establishes the Wallis product for `œÄ` (`Real.tendsto_prod_pi_div_two`). Our proof is
largely about analyzing the behaviour of the sequence `‚à´ x in 0..œÄ, sin x ^ n` as `n ‚Üí ‚àû`.
See: https://en.wikipedia.org/wiki/Wallis_product

The proof can be broken down into two pieces. The first step (carried out in
`Analysis.SpecialFunctions.Integrals`) is to use repeated integration by parts to obtain an
explicit formula for this integral, which is rational if `n` is odd and a rational multiple of `œÄ`
if `n` is even.

The second step, carried out here, is to estimate the ratio
`‚à´ (x : ‚Ñù) in 0..œÄ, sin x ^ (2 * k + 1) / ‚à´ (x : ‚Ñù) in 0..œÄ, sin x ^ (2 * k)` and prove that
it converges to one using the squeeze theorem. The final product for `œÄ` is obtained after some
algebraic manipulation.

## Main statements

* `Real.Wallis.W`: the product of the first `k` terms in Wallis' formula for `œÄ`.
* `Real.Wallis.W_eq_integral_sin_pow_div_integral_sin_pow`: express `W n` as a ratio of integrals.
* `Real.Wallis.W_le` and `Real.Wallis.le_W`: upper and lower bounds for `W n`.
* `Real.tendsto_prod_pi_div_two`: the Wallis product formula.
-/


open scoped Real Topology Nat

open Filter Finset intervalIntegral

namespace Real

namespace Wallis


/-- The product of the first `k` terms in Wallis' formula for `œÄ`. -/
noncomputable def W (k : ‚Ñï) : ‚Ñù :=
  ‚àè i ‚àà range k, (2 * i + 2) / (2 * i + 1) * ((2 * i + 2) / (2 * i + 3))

theorem W_succ (k : ‚Ñï) :
    W (k + 1) = W k * ((2 * k + 2) / (2 * k + 1) * ((2 * k + 2) / (2 * k + 3))) :=
  prod_range_succ _ _

theorem W_pos (k : ‚Ñï) : 0 < W k := by
  induction' k with k hk
  ¬∑ unfold W; simp
  ¬∑ rw [W_succ]
    refine mul_pos hk (mul_pos (div_pos ?_ ?_) (div_pos ?_ ?_)) <;> positivity

theorem W_eq_factorial_ratio (n : ‚Ñï) :
    W n = 2 ^ (4 * n) * n ! ^ 4 / ((2 * n)! ^ 2 * (2 * n + 1)) := by
  induction' n with n IH
  ¬∑ simp only [W, prod_range_zero, Nat.factorial_zero, mul_zero, pow_zero,
      algebraMap.coe_one, one_pow, mul_one, algebraMap.coe_zero, zero_add, div_self, Ne,
      one_ne_zero, not_false_iff]
    norm_num
  ¬∑ unfold W at IH ‚ä¢
    rw [prod_range_succ, IH, _root_.div_mul_div_comm, _root_.div_mul_div_comm]
    refine (div_eq_div_iff ?_ ?_).mpr ?_
    any_goals exact ne_of_gt (by positivity)
    simp_rw [Nat.mul_succ, Nat.factorial_succ, pow_succ]
    push_cast
    ring_nf

theorem W_eq_integral_sin_pow_div_integral_sin_pow (k : ‚Ñï) : (œÄ / 2)‚Åª¬π * W k =
    (‚à´ x : ‚Ñù in (0)..œÄ, sin x ^ (2 * k + 1)) / ‚à´ x : ‚Ñù in (0)..œÄ, sin x ^ (2 * k) := by
  rw [integral_sin_pow_even, integral_sin_pow_odd, mul_div_mul_comm, ‚Üê prod_div_distrib, inv_div]
  simp_rw [div_div_div_comm, div_div_eq_mul_div, mul_div_assoc]
  rfl

theorem W_le (k : ‚Ñï) : W k ‚â§ œÄ / 2 := by
  rw [‚Üê div_le_one pi_div_two_pos, div_eq_inv_mul]
  rw [W_eq_integral_sin_pow_div_integral_sin_pow, div_le_one (integral_sin_pow_pos _)]
  apply integral_sin_pow_succ_le

theorem le_W (k : ‚Ñï) : ((2 : ‚Ñù) * k + 1) / (2 * k + 2) * (œÄ / 2) ‚â§ W k := by
  rw [‚Üê le_div_iff‚ÇÄ pi_div_two_pos, div_eq_inv_mul (W k) _]
  rw [W_eq_integral_sin_pow_div_integral_sin_pow, le_div_iff‚ÇÄ (integral_sin_pow_pos _)]
  convert integral_sin_pow_succ_le (2 * k + 1)
  rw [integral_sin_pow (2 * k)]
  simp

theorem tendsto_W_nhds_pi_div_two : Tendsto W atTop (ùìù <| œÄ / 2) := by
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le ?_ tendsto_const_nhds le_W W_le
  have : ùìù (œÄ / 2) = ùìù ((1 - 0) * (œÄ / 2)) := by rw [sub_zero, one_mul]
  rw [this]
  refine Tendsto.mul ?_ tendsto_const_nhds
  have h : ‚àÄ n : ‚Ñï, ((2 : ‚Ñù) * n + 1) / (2 * n + 2) = 1 - 1 / (2 * n + 2) := by
    intro n
    rw [sub_div' (ne_of_gt (add_pos_of_nonneg_of_pos (mul_nonneg
      (two_pos : 0 < (2 : ‚Ñù)).le (Nat.cast_nonneg _)) two_pos)), one_mul]
    congr 1; ring
  simp_rw [h]
  refine (tendsto_const_nhds.div_atTop ?_).const_sub _
  refine Tendsto.atTop_add ?_ tendsto_const_nhds
  exact tendsto_natCast_atTop_atTop.const_mul_atTop two_pos

end Wallis

end Real

/-- Wallis' product formula for `œÄ / 2`. -/
theorem Real.tendsto_prod_pi_div_two :
    Tendsto (fun k => ‚àè i ‚àà range k, ((2 : ‚Ñù) * i + 2) / (2 * i + 1) * ((2 * i + 2) / (2 * i + 3)))
      atTop (ùìù (œÄ / 2)) :=
  Real.Wallis.tendsto_W_nhds_pi_div_two
