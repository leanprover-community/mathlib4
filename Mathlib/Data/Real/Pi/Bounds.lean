/-
Copyright (c) 2019 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, Mario Carneiro
-/
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Bounds

#align_import data.real.pi.bounds from "leanprover-community/mathlib"@"402f8982dddc1864bd703da2d6e2ee304a866973"

/-!
# Pi

This file contains lemmas which establish bounds on `real.pi`.
Notably, these include `pi_gt_sqrtTwoAddSeries` and `pi_lt_sqrtTwoAddSeries`,
which bound `œÄ` using series;
numerical bounds on `œÄ` such as `pi_gt_314`and `pi_lt_315` (more precise versions are given, too).

See also `Mathlib/Data/Real/Pi/Leibniz.lean` and `Mathlib/Data/Real/Pi/Wallis.lean` for infinite
formulas for `œÄ`.
-/

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220

-- Porting note: needed to add a lot of type ascriptions for lean to interpret numbers as reals.

open scoped Real

namespace Real

theorem pi_gt_sqrtTwoAddSeries (n : ‚Ñï) :
    (2 : ‚Ñù) ^ (n + 1) * sqrt (2 - sqrtTwoAddSeries 0 n) < œÄ := by
  have : sqrt (2 - sqrtTwoAddSeries 0 n) / (2 : ‚Ñù) * (2 : ‚Ñù) ^ (n + 2) < œÄ := by
    rw [‚Üê lt_div_iff, ‚Üê sin_pi_over_two_pow_succ]; apply sin_lt; apply div_pos pi_pos
    all_goals apply pow_pos; norm_num
  apply lt_of_le_of_lt (le_of_eq _) this
  -- ‚ä¢ 2 ^ (n + 1) * sqrt (2 - sqrtTwoAddSeries 0 n) = sqrt (2 - sqrtTwoAddSeries 0 ‚Ä¶
  rw [pow_succ _ (n + 1), ‚Üê mul_assoc, div_mul_cancel, mul_comm]; norm_num
  -- ‚ä¢ 2 ‚â† 0
                                                                  -- üéâ no goals
#align real.pi_gt_sqrt_two_add_series Real.pi_gt_sqrtTwoAddSeries

theorem pi_lt_sqrtTwoAddSeries (n : ‚Ñï) :
    œÄ < (2 : ‚Ñù) ^ (n + 1) * sqrt (2 - sqrtTwoAddSeries 0 n) + 1 / (4 : ‚Ñù) ^ n := by
  have : œÄ <
      (sqrt (2 - sqrtTwoAddSeries 0 n) / (2 : ‚Ñù) + (1 : ‚Ñù) / ((2 : ‚Ñù) ^ n) ^ 3 / 4) *
      (2 : ‚Ñù) ^ (n + 2) := by
    rw [‚Üê div_lt_iff, ‚Üê sin_pi_over_two_pow_succ]
    refine' lt_of_lt_of_le (lt_add_of_sub_right_lt (sin_gt_sub_cube _ _)) _
    ¬∑ apply div_pos pi_pos; apply pow_pos; norm_num
    ¬∑ rw [div_le_iff']
      ¬∑ refine' le_trans pi_le_four _
        simp only [show (4 : ‚Ñù) = (2 : ‚Ñù) ^ 2 by norm_num, mul_one]
        apply pow_le_pow; norm_num; apply le_add_of_nonneg_left; apply Nat.zero_le
      ¬∑ apply pow_pos; norm_num
    apply add_le_add_left; rw [div_le_div_right]
    rw [le_div_iff, ‚Üê mul_pow]
    refine' le_trans _ (le_of_eq (one_pow 3)); apply pow_le_pow_of_le_left
    ¬∑ apply le_of_lt; apply mul_pos; apply div_pos pi_pos; apply pow_pos; norm_num; apply pow_pos
      norm_num
    rw [‚Üê le_div_iff]
    refine' le_trans ((div_le_div_right _).mpr pi_le_four) _; apply pow_pos; norm_num
    rw [pow_succ, pow_succ, ‚Üê mul_assoc, ‚Üê div_div]
    -- Porting note: removed `convert le_rfl`
    all_goals (repeat' apply pow_pos); norm_num
  apply lt_of_lt_of_le this (le_of_eq _); rw [add_mul]; congr 1
  -- ‚ä¢ (sqrt (2 - sqrtTwoAddSeries 0 n) / 2 + 1 / (2 ^ n) ^ 3 / 4) * 2 ^ (n + 2) =  ‚Ä¶
                                          -- ‚ä¢ sqrt (2 - sqrtTwoAddSeries 0 n) / 2 * 2 ^ (n + 2) + 1 / (2 ^ n) ^ 3 / 4 * 2  ‚Ä¶
                                                        -- ‚ä¢ sqrt (2 - sqrtTwoAddSeries 0 n) / 2 * 2 ^ (n + 2) = 2 ^ (n + 1) * sqrt (2 -  ‚Ä¶
  ¬∑ rw [pow_succ _ (n + 1), ‚Üê mul_assoc, div_mul_cancel, mul_comm]; norm_num
    -- ‚ä¢ 2 ‚â† 0
                                                                    -- üéâ no goals
  rw [pow_succ, ‚Üê pow_mul, mul_comm n 2, pow_mul, show (2 : ‚Ñù) ^ 2 = 4 by norm_num, pow_succ,
    pow_succ, ‚Üê mul_assoc (2 : ‚Ñù), show (2 : ‚Ñù) * 2 = 4 by norm_num, ‚Üê mul_assoc, div_mul_cancel,
    mul_comm ((2 : ‚Ñù) ^ n), ‚Üê div_div, div_mul_cancel]
  apply pow_ne_zero; norm_num; norm_num
  -- ‚ä¢ 2 ‚â† 0
                     -- ‚ä¢ 4 ‚â† 0
                               -- üéâ no goals
#align real.pi_lt_sqrt_two_add_series Real.pi_lt_sqrtTwoAddSeries

/-- From an upper bound on `sqrtTwoAddSeries 0 n = 2 cos (œÄ / 2 ^ (n+1))` of the form
`sqrtTwoAddSeries 0 n ‚â§ 2 - (a / 2 ^ (n + 1)) ^ 2)`, one can deduce the lower bound `a < œÄ`
thanks to basic trigonometric inequalities as expressed in `pi_gt_sqrtTwoAddSeries`. -/
theorem pi_lower_bound_start (n : ‚Ñï) {a}
    (h : sqrtTwoAddSeries ((0 : ‚Ñï) / (1 : ‚Ñï)) n ‚â§ (2 : ‚Ñù) - (a / (2 : ‚Ñù) ^ (n + 1)) ^ 2) :
    a < œÄ := by
  refine' lt_of_le_of_lt _ (pi_gt_sqrtTwoAddSeries n); rw [mul_comm]
  -- ‚ä¢ a ‚â§ 2 ^ (n + 1) * sqrt (2 - sqrtTwoAddSeries 0 n)
                                                       -- ‚ä¢ a ‚â§ sqrt (2 - sqrtTwoAddSeries 0 n) * 2 ^ (n + 1)
  refine' (div_le_iff (pow_pos (by norm_num) _ : (0 : ‚Ñù) < _)).mp (le_sqrt_of_sq_le _)
  -- ‚ä¢ (a / 2 ^ (n + 1)) ^ 2 ‚â§ 2 - sqrtTwoAddSeries 0 n
  rwa [le_sub_comm, show (0 : ‚Ñù) = (0 : ‚Ñï) / (1 : ‚Ñï) by rw [Nat.cast_zero, zero_div]]
  -- üéâ no goals
#align real.pi_lower_bound_start Real.pi_lower_bound_start

theorem sqrtTwoAddSeries_step_up (c d : ‚Ñï) {a b n : ‚Ñï} {z : ‚Ñù} (hz : sqrtTwoAddSeries (c / d) n ‚â§ z)
    (hb : 0 < b) (hd : 0 < d) (h : (2 * b + a) * d ^ 2 ‚â§ c ^ 2 * b) :
    sqrtTwoAddSeries (a / b) (n + 1) ‚â§ z := by
  refine' le_trans _ hz; rw [sqrtTwoAddSeries_succ]; apply sqrtTwoAddSeries_monotone_left
  -- ‚ä¢ sqrtTwoAddSeries (‚Üëa / ‚Üëb) (n + 1) ‚â§ sqrtTwoAddSeries (‚Üëc / ‚Üëd) n
                         -- ‚ä¢ sqrtTwoAddSeries (sqrt (2 + ‚Üëa / ‚Üëb)) n ‚â§ sqrtTwoAddSeries (‚Üëc / ‚Üëd) n
                                                     -- ‚ä¢ sqrt (2 + ‚Üëa / ‚Üëb) ‚â§ ‚Üëc / ‚Üëd
  have hb' : 0 < (b : ‚Ñù) := Nat.cast_pos.2 hb
  -- ‚ä¢ sqrt (2 + ‚Üëa / ‚Üëb) ‚â§ ‚Üëc / ‚Üëd
  have hd' : 0 < (d : ‚Ñù) := Nat.cast_pos.2 hd
  -- ‚ä¢ sqrt (2 + ‚Üëa / ‚Üëb) ‚â§ ‚Üëc / ‚Üëd
  rw [sqrt_le_left (div_nonneg c.cast_nonneg d.cast_nonneg), div_pow,
    add_div_eq_mul_add_div _ _ (ne_of_gt hb'), div_le_div_iff hb' (pow_pos hd' _)]
  exact_mod_cast h
  -- üéâ no goals
#align real.sqrt_two_add_series_step_up Real.sqrtTwoAddSeries_step_up

section Tactic

open Lean Elab Tactic

/-- `numDen stx` takes a syntax expression `stx` and
* if it is of the form `a / b`, then it returns `some (a, b)`;
* otherwise it returns `none`.
-/
private def numDen : Syntax ‚Üí Option (Syntax.Term √ó Syntax.Term)
  | `($a / $b) => some (a, b)
  | _          => none

/-- Create a proof of `a < œÄ` for a fixed rational number `a`, given a witness, which is a
sequence of rational numbers `sqrt 2 < r 1 < r 2 < ... < r n < 2` satisfying the property that
`sqrt (2 + r i) ‚â§ r(i+1)`, where `r 0 = 0` and `sqrt (2 - r n) ‚â• a/2^(n+1)`. -/
elab "pi_lower_bound " "[" l:term,* "]" : tactic => do
  let rat_sep := l.elemsAndSeps
  let sep := rat_sep.getD 1 .missing
  let ratStx := rat_sep.filter (¬∑ != sep)
  let n := ‚Üê (toExpr ratStx.size).toSyntax
  let els := (ratStx.map numDen).reduceOption
  evalTactic (‚Üê `(tactic| apply pi_lower_bound_start $n))
  let _ := ‚Üê els.mapM fun (x, y) => do
      evalTactic (‚Üê `(tactic| apply sqrtTwoAddSeries_step_up $x $y))
  evalTactic (‚Üê `(tactic| simp [sqrtTwoAddSeries]))
  allGoals (evalTactic (‚Üê `(tactic| norm_num1)))

end Tactic

/-- From a lower bound on `sqrtTwoAddSeries 0 n = 2 cos (œÄ / 2 ^ (n+1))` of the form
`2 - ((a - 1 / 4 ^ n) / 2 ^ (n + 1)) ^ 2 ‚â§ sqrtTwoAddSeries 0 n`, one can deduce the upper bound
`œÄ < a` thanks to basic trigonometric formulas as expressed in `pi_lt_sqrtTwoAddSeries`. -/
theorem pi_upper_bound_start (n : ‚Ñï) {a}
    (h : (2 : ‚Ñù) - ((a - 1 / (4 : ‚Ñù) ^ n) / (2 : ‚Ñù) ^ (n + 1)) ^ 2 ‚â§
        sqrtTwoAddSeries ((0 : ‚Ñï) / (1 : ‚Ñï)) n)
    (h‚ÇÇ : (1 : ‚Ñù) / (4 : ‚Ñù) ^ n ‚â§ a) : œÄ < a := by
  refine' lt_of_lt_of_le (pi_lt_sqrtTwoAddSeries n) _
  -- ‚ä¢ 2 ^ (n + 1) * sqrt (2 - sqrtTwoAddSeries 0 n) + 1 / 4 ^ n ‚â§ a
  rw [‚Üê le_sub_iff_add_le, ‚Üê le_div_iff', sqrt_le_left, sub_le_comm]
  ¬∑ rwa [Nat.cast_zero, zero_div] at h
    -- üéâ no goals
  ¬∑ exact div_nonneg (sub_nonneg.2 h‚ÇÇ) (pow_nonneg (le_of_lt zero_lt_two) _)
    -- üéâ no goals
  ¬∑ exact pow_pos zero_lt_two _
    -- üéâ no goals
#align real.pi_upper_bound_start Real.pi_upper_bound_start

theorem sqrtTwoAddSeries_step_down (a b : ‚Ñï) {c d n : ‚Ñï} {z : ‚Ñù}
    (hz : z ‚â§ sqrtTwoAddSeries (a / b) n) (hb : 0 < b) (hd : 0 < d)
    (h : a ^ 2 * d ‚â§ (2 * d + c) * b ^ 2) : z ‚â§ sqrtTwoAddSeries (c / d) (n + 1) := by
  apply le_trans hz; rw [sqrtTwoAddSeries_succ]; apply sqrtTwoAddSeries_monotone_left
  -- ‚ä¢ sqrtTwoAddSeries (‚Üëa / ‚Üëb) n ‚â§ sqrtTwoAddSeries (‚Üëc / ‚Üëd) (n + 1)
                     -- ‚ä¢ sqrtTwoAddSeries (‚Üëa / ‚Üëb) n ‚â§ sqrtTwoAddSeries (sqrt (2 + ‚Üëc / ‚Üëd)) n
                                                 -- ‚ä¢ ‚Üëa / ‚Üëb ‚â§ sqrt (2 + ‚Üëc / ‚Üëd)
  apply le_sqrt_of_sq_le
  -- ‚ä¢ (‚Üëa / ‚Üëb) ^ 2 ‚â§ 2 + ‚Üëc / ‚Üëd
  have hb' : 0 < (b : ‚Ñù) := Nat.cast_pos.2 hb
  -- ‚ä¢ (‚Üëa / ‚Üëb) ^ 2 ‚â§ 2 + ‚Üëc / ‚Üëd
  have hd' : 0 < (d : ‚Ñù) := Nat.cast_pos.2 hd
  -- ‚ä¢ (‚Üëa / ‚Üëb) ^ 2 ‚â§ 2 + ‚Üëc / ‚Üëd
  rw [div_pow, add_div_eq_mul_add_div _ _ (ne_of_gt hd'), div_le_div_iff (pow_pos hb' _) hd']
  -- ‚ä¢ ‚Üëa ^ 2 * ‚Üëd ‚â§ (2 * ‚Üëd + ‚Üëc) * ‚Üëb ^ 2
  exact_mod_cast h
  -- üéâ no goals
#align real.sqrt_two_add_series_step_down Real.sqrtTwoAddSeries_step_down

section Tactic

open Lean Elab Tactic

/-- Create a proof of `œÄ < a` for a fixed rational number `a`, given a witness, which is a
sequence of rational numbers `sqrt 2 < r 1 < r 2 < ... < r n < 2` satisfying the property that
`sqrt (2 + r i) ‚â• r(i+1)`, where `r 0 = 0` and `sqrt (2 - r n) ‚â• (a - 1/4^n) / 2^(n+1)`. -/
elab "pi_upper_bound " "[" l:term,* "]" : tactic => do
  let rat_sep := l.elemsAndSeps
  let sep := rat_sep.getD 1 .missing
  let ratStx := rat_sep.filter (¬∑ != sep)
  let n := ‚Üê (toExpr ratStx.size).toSyntax
  let els := (ratStx.map numDen).reduceOption
  evalTactic (‚Üê `(tactic| apply pi_upper_bound_start $n))
  let _ := ‚Üê els.mapM fun (x, y) => do
      evalTactic (‚Üê `(tactic| apply sqrtTwoAddSeries_step_down $x $y))
  evalTactic (‚Üê `(tactic| simp [sqrtTwoAddSeries]))
  allGoals (evalTactic (‚Üê `(tactic| norm_num1)))

end Tactic

theorem pi_gt_three : 3 < œÄ := by
  pi_lower_bound [23/16]
  -- üéâ no goals
#align real.pi_gt_three Real.pi_gt_three

theorem pi_gt_314 : 3.14 < œÄ := by
  pi_lower_bound [99 / 70, 874 / 473, 1940 / 989, 1447 / 727]
  -- üéâ no goals
#align real.pi_gt_314 Real.pi_gt_314

theorem pi_lt_315 : œÄ < 3.15 := by
  pi_upper_bound [140 / 99, 279 / 151, 51 / 26, 412 / 207]
  -- üéâ no goals
#align real.pi_lt_315 Real.pi_lt_315

theorem pi_gt_31415 : 3.1415 < œÄ := by
  pi_lower_bound
        [11482 / 8119, 5401 / 2923, 2348 / 1197, 11367 / 5711, 25705 / 12868, 23235 / 11621]
#align real.pi_gt_31415 Real.pi_gt_31415

theorem pi_lt_31416 : œÄ < 3.1416 := by
    pi_upper_bound
        [4756 / 3363, 101211 / 54775, 505534 / 257719, 83289 / 41846, 411278 / 205887,
          438142 / 219137, 451504 / 225769, 265603 / 132804, 849938 / 424971]
#align real.pi_lt_31416 Real.pi_lt_31416

theorem pi_gt_3141592 : 3.141592 < œÄ := by
    pi_lower_bound
        [11482 / 8119, 7792 / 4217, 54055 / 27557, 949247 / 476920, 3310126 / 1657059,
          2635492 / 1318143, 1580265 / 790192, 1221775 / 610899, 3612247 / 1806132, 849943 / 424972]
#align real.pi_gt_3141592 Real.pi_gt_3141592

theorem pi_lt_3141593 : œÄ < 3.141593 := by
    pi_upper_bound
        [27720 / 19601, 56935 / 30813, 49359 / 25163, 258754 / 130003, 113599 / 56868,
          1101994 / 551163, 8671537 / 4336095, 3877807 / 1938940, 52483813 / 26242030,
          56946167 / 28473117, 23798415 / 11899211]
#align real.pi_lt_3141593 Real.pi_lt_3141593

end Real
