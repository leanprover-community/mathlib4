/-
Copyright (c) 2020 Benjamin Davidson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Benjamin Davidson, Jeremy Tan
-/
import Mathlib.Analysis.Complex.AbelLimit
import Mathlib.Analysis.SpecialFunctions.Complex.Arctan

#align_import data.real.pi.leibniz from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-! ### Leibniz's series for `Ï€` -/

namespace Real

open Filter Finset

open scoped Topology

/-- **Leibniz's series for `Ï€`**. The alternating sum of odd number reciprocals is `Ï€ / 4`,
proved by using Abel's limit theorem to extend the Maclaurin series of `arctan` to 1. -/
theorem tendsto_sum_pi_div_four :
    Tendsto (fun k => âˆ‘ i âˆˆ range k, (-1 : â„) ^ i / (2 * i + 1)) atTop (ğ“ (Ï€ / 4)) := by
  -- The series is alternating with terms of decreasing magnitude, so it converges to some limit
  obtain âŸ¨l, hâŸ© :
      âˆƒ l, Tendsto (fun n â†¦ âˆ‘ i âˆˆ range n, (-1 : â„) ^ i / (2 * i + 1)) atTop (ğ“ l) := by
    apply Antitone.tendsto_alternating_series_of_tendsto_zero
    Â· exact antitone_iff_forall_lt.mpr fun _ _ _ â†¦ by gcongr
    Â· apply Tendsto.inv_tendsto_atTop; apply tendsto_atTop_add_const_right
      exact tendsto_natCast_atTop_atTop.const_mul_atTop zero_lt_two
  -- Abel's limit theorem states that the corresponding power series has the same limit as `x â†’ 1â»`
  have abel := tendsto_tsum_powerSeries_nhdsWithin_lt h
  -- Massage the expression to get `x ^ (2 * n + 1)` in the tsum rather than `x ^ n`...
  have m : ğ“[<] (1 : â„) â‰¤ ğ“ 1 := tendsto_nhdsWithin_of_tendsto_nhds fun _ a â†¦ a
  have q : Tendsto (fun x : â„ â†¦ x ^ 2) (ğ“[<] 1) (ğ“[<] 1) := by
    apply tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within
    Â· nth_rw 3 [â† one_pow 2]
      exact Tendsto.pow â€¹_â€º _
    Â· rw [eventually_iff_exists_mem]
      use Set.Ioo (-1) 1
      exact âŸ¨(by rw [mem_nhdsWithin_Iio_iff_exists_Ioo_subset]; use -1, by simp),
        fun _ _ â†¦ by rwa [Set.mem_Iio, sq_lt_one_iff_abs_lt_one, abs_lt, â† Set.mem_Ioo]âŸ©
  replace abel := (abel.comp q).mul m
  rw [mul_one] at abel
  -- ...so that we can replace the tsum with the real arctangent function
  replace abel : Tendsto arctan (ğ“[<] 1) (ğ“ l) := by
    apply abel.congr'
    rw [eventuallyEq_nhdsWithin_iff, Metric.eventually_nhds_iff]
    use 1, zero_lt_one
    intro y hy1 hy2
    rw [dist_eq, abs_sub_lt_iff] at hy1
    rw [Set.mem_Iio] at hy2
    have ny : â€–yâ€– < 1 := by rw [norm_eq_abs, abs_lt]; constructor <;> linarith
    rw [â† (hasSum_arctan ny).tsum_eq, Function.comp_apply, â† tsum_mul_right]
    simp_rw [mul_assoc, â† pow_mul, â† pow_succ, div_mul_eq_mul_div]
    norm_cast
  -- But `arctan` is continuous everywhere, so the limit is `arctan 1 = Ï€ / 4`
  rwa [tendsto_nhds_unique abel ((continuous_arctan.tendsto 1).mono_left m), arctan_one] at h
#align real.tendsto_sum_pi_div_four Real.tendsto_sum_pi_div_four

end Real
