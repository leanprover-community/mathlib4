/-
Copyright (c) 2018 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Robert Y. Lewis
-/
import Mathlib.Data.Real.CauSeq

#align_import data.real.cau_seq_completion from "leanprover-community/mathlib"@"cf4c49c445991489058260d75dae0ff2b1abca28"

/-!
# Cauchy completion

This file generalizes the Cauchy completion of `(â„š, abs)` to the completion of a ring
with absolute value.
-/


namespace CauSeq.Completion

open CauSeq

section

variable {Î± : Type*} [LinearOrderedField Î±]

variable {Î² : Type*} [Ring Î²] (abv : Î² â†’ Î±) [IsAbsoluteValue abv]

-- TODO: rename this to `CauSeq.Completion` instead of `CauSeq.Completion.Cauchy`.
/-- The Cauchy completion of a ring with absolute value. -/
def Cauchy :=
  @Quotient (CauSeq _ abv) CauSeq.equiv
set_option linter.uppercaseLean3 false in
#align cau_seq.completion.Cauchy CauSeq.Completion.Cauchy

variable {abv}

/-- The map from Cauchy sequences into the Cauchy completion. -/
def mk : CauSeq _ abv â†’ Cauchy abv :=
  Quotient.mk''
#align cau_seq.completion.mk CauSeq.Completion.mk

@[simp]
theorem mk_eq_mk (f : CauSeq _ abv) : @Eq (Cauchy abv) âŸ¦fâŸ§ (mk f) :=
  rfl
#align cau_seq.completion.mk_eq_mk CauSeq.Completion.mk_eq_mk

theorem mk_eq {f g : CauSeq _ abv} : mk f = mk g â†” f â‰ˆ g :=
  Quotient.eq
#align cau_seq.completion.mk_eq CauSeq.Completion.mk_eq

/-- The map from the original ring into the Cauchy completion. -/
def ofRat (x : Î²) : Cauchy abv :=
  mk (const abv x)
#align cau_seq.completion.of_rat CauSeq.Completion.ofRat

instance : Zero (Cauchy abv) :=
  âŸ¨ofRat 0âŸ©

instance : One (Cauchy abv) :=
  âŸ¨ofRat 1âŸ©

instance : Inhabited (Cauchy abv) :=
  âŸ¨0âŸ©

theorem ofRat_zero : (ofRat 0 : Cauchy abv) = 0 :=
  rfl
#align cau_seq.completion.of_rat_zero CauSeq.Completion.ofRat_zero

theorem ofRat_one : (ofRat 1 : Cauchy abv) = 1 :=
  rfl
#align cau_seq.completion.of_rat_one CauSeq.Completion.ofRat_one

@[simp]
theorem mk_eq_zero {f : CauSeq _ abv} : mk f = 0 â†” LimZero f := by
  have : mk f = 0 â†” LimZero (f - 0) := Quotient.eq
  -- âŠ¢ mk f = 0 â†” LimZero f
  rwa [sub_zero] at this
  -- ğŸ‰ no goals
#align cau_seq.completion.mk_eq_zero CauSeq.Completion.mk_eq_zero

instance : Add (Cauchy abv) :=
  âŸ¨(Quotient.mapâ‚‚ (Â· + Â·)) fun _ _ hf _ _ hg => add_equiv_add hf hgâŸ©

@[simp]
theorem mk_add (f g : CauSeq Î² abv) : mk f + mk g = mk (f + g) :=
  rfl
#align cau_seq.completion.mk_add CauSeq.Completion.mk_add

instance : Neg (Cauchy abv) :=
  âŸ¨(Quotient.map Neg.neg) fun _ _ hf => neg_equiv_neg hfâŸ©

@[simp]
theorem mk_neg (f : CauSeq Î² abv) : -mk f = mk (-f) :=
  rfl
#align cau_seq.completion.mk_neg CauSeq.Completion.mk_neg

instance : Mul (Cauchy abv) :=
  âŸ¨(Quotient.mapâ‚‚ (Â· * Â·)) fun _ _ hf _ _ hg => mul_equiv_mul hf hgâŸ©

@[simp]
theorem mk_mul (f g : CauSeq Î² abv) : mk f * mk g = mk (f * g) :=
  rfl
#align cau_seq.completion.mk_mul CauSeq.Completion.mk_mul

instance : Sub (Cauchy abv) :=
  âŸ¨(Quotient.mapâ‚‚ Sub.sub) fun _ _ hf _ _ hg => sub_equiv_sub hf hgâŸ©

@[simp]
theorem mk_sub (f g : CauSeq Î² abv) : mk f - mk g = mk (f - g) :=
  rfl
#align cau_seq.completion.mk_sub CauSeq.Completion.mk_sub

instance {Î³ : Type*} [SMul Î³ Î²] [IsScalarTower Î³ Î² Î²] : SMul Î³ (Cauchy abv) :=
  âŸ¨fun c => (Quotient.map ((Â· â€¢ Â·) c)) fun _ _ hf => smul_equiv_smul _ hfâŸ©

@[simp]
theorem mk_smul {Î³ : Type*} [SMul Î³ Î²] [IsScalarTower Î³ Î² Î²] (c : Î³) (f : CauSeq Î² abv) :
    c â€¢ mk f = mk (c â€¢ f) :=
  rfl
#align cau_seq.completion.mk_smul CauSeq.Completion.mk_smul

instance : Pow (Cauchy abv) â„• :=
  âŸ¨fun x n => Quotient.map (Â· ^ n) (fun _ _ hf => pow_equiv_pow hf _) xâŸ©

@[simp]
theorem mk_pow (n : â„•) (f : CauSeq Î² abv) : mk f ^ n = mk (f ^ n) :=
  rfl
#align cau_seq.completion.mk_pow CauSeq.Completion.mk_pow

instance : NatCast (Cauchy abv) :=
  âŸ¨fun n => mk nâŸ©

instance : IntCast (Cauchy abv) :=
  âŸ¨fun n => mk nâŸ©

@[simp]
theorem ofRat_natCast (n : â„•) : (ofRat n : Cauchy abv) = n :=
  rfl
#align cau_seq.completion.of_rat_nat_cast CauSeq.Completion.ofRat_natCast

@[simp]
theorem ofRat_intCast (z : â„¤) : (ofRat z : Cauchy abv) = z :=
  rfl
#align cau_seq.completion.of_rat_int_cast CauSeq.Completion.ofRat_intCast

theorem ofRat_add (x y : Î²) :
    ofRat (x + y) = (ofRat x + ofRat y : Cauchy abv) :=
  congr_arg mk (const_add _ _)
#align cau_seq.completion.of_rat_add CauSeq.Completion.ofRat_add

theorem ofRat_neg (x : Î²) : ofRat (-x) = (-ofRat x : Cauchy abv) :=
  congr_arg mk (const_neg _)
#align cau_seq.completion.of_rat_neg CauSeq.Completion.ofRat_neg

theorem ofRat_mul (x y : Î²) :
    ofRat (x * y) = (ofRat x * ofRat y : Cauchy abv) :=
  congr_arg mk (const_mul _ _)
#align cau_seq.completion.of_rat_mul CauSeq.Completion.ofRat_mul

private theorem zero_def : 0 = @mk _ _ _ _ abv _ 0 :=
  rfl

private theorem one_def : 1 = @mk _ _ _ _ abv _ 1 :=
  rfl

instance Cauchy.ring : Ring (Cauchy abv) :=
  Function.Surjective.ring mk (surjective_quotient_mk _) zero_def.symm one_def.symm
    (fun _ _ => (mk_add _ _).symm) (fun _ _ => (mk_mul _ _).symm) (fun _ => (mk_neg _).symm)
    (fun _ _ => (mk_sub _ _).symm) (fun _ _ => (mk_smul _ _).symm) (fun _ _ => (mk_smul _ _).symm)
    (fun _ _ => (mk_pow _ _).symm) (fun _ => rfl) fun _ => rfl

/-- `CauSeq.Completion.ofRat` as a `RingHom` -/
@[simps]
def ofRatRingHom : Î² â†’+* (Cauchy abv) where
  toFun := ofRat
  map_zero' := ofRat_zero
  map_one' := ofRat_one
  map_add' := ofRat_add
  map_mul' := ofRat_mul
#align cau_seq.completion.of_rat_ring_hom CauSeq.Completion.ofRatRingHom
#align cau_seq.completion.of_rat_ring_hom_apply CauSeq.Completion.ofRatRingHom_apply

theorem ofRat_sub (x y : Î²) : ofRat (x - y) = (ofRat x - ofRat y : Cauchy abv) :=
  congr_arg mk (const_sub _ _)
#align cau_seq.completion.of_rat_sub CauSeq.Completion.ofRat_sub

end

section

variable {Î± : Type*} [LinearOrderedField Î±]

variable {Î² : Type*} [CommRing Î²] {abv : Î² â†’ Î±} [IsAbsoluteValue abv]

instance Cauchy.commRing : CommRing (Cauchy abv) :=
  Function.Surjective.commRing mk (surjective_quotient_mk _) zero_def.symm one_def.symm
    (fun _ _ => (mk_add _ _).symm) (fun _ _ => (mk_mul _ _).symm) (fun _ => (mk_neg _).symm)
    (fun _ _ => (mk_sub _ _).symm) (fun _ _ => (mk_smul _ _).symm) (fun _ _ => (mk_smul _ _).symm)
    (fun _ _ => (mk_pow _ _).symm) (fun _ => rfl) fun _ => rfl

end

open Classical

section

variable {Î± : Type*} [LinearOrderedField Î±]

variable {Î² : Type*} [DivisionRing Î²] {abv : Î² â†’ Î±} [IsAbsoluteValue abv]

instance : RatCast (Cauchy abv) :=
  âŸ¨fun q => ofRat qâŸ©

@[simp, coe]
theorem ofRat_ratCast (q : â„š) : ofRat (â†‘q : Î²) = (q : (Cauchy abv)) :=
  rfl
#align cau_seq.completion.of_rat_rat_cast CauSeq.Completion.ofRat_ratCast

noncomputable instance : Inv (Cauchy abv) :=
  âŸ¨fun x =>
    (Quotient.liftOn x fun f => mk <| if h : LimZero f then 0 else inv f h) fun f g fg => by
      have := limZero_congr fg
      -- âŠ¢ mk (if h : LimZero f then 0 else inv f h) = mk (if h : LimZero g then 0 else â€¦
      by_cases hf : LimZero f
      -- âŠ¢ mk (if h : LimZero f then 0 else inv f h) = mk (if h : LimZero g then 0 else â€¦
      Â· simp [hf, this.1 hf, Setoid.refl]
        -- ğŸ‰ no goals
      Â· have hg := mt this.2 hf
        -- âŠ¢ mk (if h : LimZero f then 0 else inv f h) = mk (if h : LimZero g then 0 else â€¦
        simp [hf, hg]
        -- âŠ¢ mk (inv f (_ : Â¬LimZero f)) = mk (inv g (_ : Â¬LimZero g))
        have If : mk (inv f hf) * mk f = 1 := mk_eq.2 (inv_mul_cancel hf)
        -- âŠ¢ mk (inv f (_ : Â¬LimZero f)) = mk (inv g (_ : Â¬LimZero g))
        have Ig : mk (inv g hg) * mk g = 1 := mk_eq.2 (inv_mul_cancel hg)
        -- âŠ¢ mk (inv f (_ : Â¬LimZero f)) = mk (inv g (_ : Â¬LimZero g))
        have Ig' : mk g * mk (inv g hg) = 1 := mk_eq.2 (mul_inv_cancel hg)
        -- âŠ¢ mk (inv f (_ : Â¬LimZero f)) = mk (inv g (_ : Â¬LimZero g))
        rw [mk_eq.2 fg, â† Ig] at If
        -- âŠ¢ mk (inv f (_ : Â¬LimZero f)) = mk (inv g (_ : Â¬LimZero g))
        rw [â† mul_one (mk (inv f hf)), â† Ig', â† mul_assoc, If, mul_assoc, Ig', mul_one]âŸ©
        -- ğŸ‰ no goals

-- porting note: simp can prove this
-- @[simp]
theorem inv_zero : (0 : (Cauchy abv))â»Â¹ = 0 :=
  congr_arg mk <| by rw [dif_pos] <;> [rfl; exact zero_limZero]
                     -- ğŸ‰ no goals
#align cau_seq.completion.inv_zero CauSeq.Completion.inv_zero

@[simp]
theorem inv_mk {f} (hf) : (@mk Î± _ Î² _ abv _ f)â»Â¹ = mk (inv f hf) :=
  congr_arg mk <| by rw [dif_neg]
                     -- ğŸ‰ no goals
#align cau_seq.completion.inv_mk CauSeq.Completion.inv_mk

theorem cau_seq_zero_ne_one : Â¬(0 : CauSeq _ abv) â‰ˆ 1 := fun h =>
  have : LimZero (1 - 0) := Setoid.symm h
  have : LimZero 1 := by simpa
                         -- ğŸ‰ no goals
  by apply one_ne_zero <| const_limZero.1 this
     -- ğŸ‰ no goals
#align cau_seq.completion.cau_seq_zero_ne_one CauSeq.Completion.cau_seq_zero_ne_one

theorem zero_ne_one : (0 : (Cauchy abv)) â‰  1 := fun h => cau_seq_zero_ne_one <| mk_eq.1 h
#align cau_seq.completion.zero_ne_one CauSeq.Completion.zero_ne_one

protected theorem inv_mul_cancel {x : (Cauchy abv)} : x â‰  0 â†’ xâ»Â¹ * x = 1 :=
  Quotient.inductionOn x fun f hf => by
    simp only [mk_eq_mk, ne_eq, mk_eq_zero] at hf
    -- âŠ¢ (Quotient.mk equiv f)â»Â¹ * Quotient.mk equiv f = 1
    simp [hf]
    -- âŠ¢ mk (inv f (_ : Â¬LimZero f) * f) = 1
    exact Quotient.sound (CauSeq.inv_mul_cancel hf)
    -- ğŸ‰ no goals
#align cau_seq.completion.inv_mul_cancel CauSeq.Completion.inv_mul_cancel

protected theorem mul_inv_cancel {x : (Cauchy abv)} : x â‰  0 â†’ x * xâ»Â¹ = 1 :=
  Quotient.inductionOn x fun f hf => by
    simp only [mk_eq_mk, ne_eq, mk_eq_zero] at hf
    -- âŠ¢ Quotient.mk equiv f * (Quotient.mk equiv f)â»Â¹ = 1
    simp [hf]
    -- âŠ¢ mk (f * inv f (_ : Â¬LimZero f)) = 1
    exact Quotient.sound (CauSeq.mul_inv_cancel hf)
    -- ğŸ‰ no goals
#align cau_seq.completion.mul_inv_cancel CauSeq.Completion.mul_inv_cancel

theorem ofRat_inv (x : Î²) : ofRat xâ»Â¹ = ((ofRat x)â»Â¹ : (Cauchy abv)) :=
  congr_arg mk <| by split_ifs with h <;> [simp [const_limZero.1 h]; rfl]
                     -- ğŸ‰ no goals
#align cau_seq.completion.of_rat_inv CauSeq.Completion.ofRat_inv

/- porting note: This takes a long time to compile.
   Also needed to rewrite the proof of ratCast_mk due to simp issues -/
/-- The Cauchy completion forms a division ring. -/
noncomputable instance Cauchy.divisionRing : DivisionRing (Cauchy abv) where
  exists_pair_ne := âŸ¨0, 1, zero_ne_oneâŸ©
  inv_zero := inv_zero
  mul_inv_cancel x := CauSeq.Completion.mul_inv_cancel
  ratCast q := ofRat q
  ratCast_mk n d hd hnd := by rw [â† ofRat_ratCast, Rat.cast_mk', ofRat_mul, ofRat_inv]; rfl
                              -- âŠ¢ ofRat â†‘n * (ofRat â†‘d)â»Â¹ = â†‘n * (â†‘d)â»Â¹
                                                                                        -- ğŸ‰ no goals

theorem ofRat_div (x y : Î²) : ofRat (x / y) = (ofRat x / ofRat y : Cauchy abv) := by
  simp only [div_eq_mul_inv, ofRat_inv, ofRat_mul]
  -- ğŸ‰ no goals
#align cau_seq.completion.of_rat_div CauSeq.Completion.ofRat_div

/-- Show the first 10 items of a representative of this equivalence class of cauchy sequences.

The representative chosen is the one passed in the VM to `Quot.mk`, so two cauchy sequences
converging to the same number may be printed differently.
-/
unsafe instance [Repr Î²] : Repr (Cauchy abv) where
  reprPrec r _ :=
    let N := 10
    let seq := r.unquot
    "(sorry /- " ++ Std.Format.joinSep ((List.range N).map <| repr âˆ˜ seq) ", " ++ ", ... -/)"

end

section

variable {Î± : Type*} [LinearOrderedField Î±]

variable {Î² : Type*} [Field Î²] {abv : Î² â†’ Î±} [IsAbsoluteValue abv]

/-- The Cauchy completion forms a field. -/
noncomputable instance Cauchy.field : Field (Cauchy abv) :=
  { Cauchy.divisionRing, Cauchy.commRing with }

end

end CauSeq.Completion

variable {Î± : Type*} [LinearOrderedField Î±]

namespace CauSeq

section

variable (Î² : Type*) [Ring Î²] (abv : Î² â†’ Î±) [IsAbsoluteValue abv]

/-- A class stating that a ring with an absolute value is complete, i.e. every Cauchy
sequence has a limit. -/
class IsComplete : Prop where
  /-- Every Cauchy sequence has a limit. -/
  isComplete : âˆ€ s : CauSeq Î² abv, âˆƒ b : Î², s â‰ˆ const abv b
#align cau_seq.is_complete CauSeq.IsComplete
#align cau_seq.is_complete.is_complete CauSeq.IsComplete.isComplete

end

section

variable {Î² : Type*} [Ring Î²] {abv : Î² â†’ Î±} [IsAbsoluteValue abv]

variable [IsComplete Î² abv]

theorem complete : âˆ€ s : CauSeq Î² abv, âˆƒ b : Î², s â‰ˆ const abv b :=
  IsComplete.isComplete
#align cau_seq.complete CauSeq.complete

/-- The limit of a Cauchy sequence in a complete ring. Chosen non-computably. -/
noncomputable def lim (s : CauSeq Î² abv) : Î² :=
  Classical.choose (complete s)
#align cau_seq.lim CauSeq.lim

theorem equiv_lim (s : CauSeq Î² abv) : s â‰ˆ const abv (lim s) :=
  Classical.choose_spec (complete s)
#align cau_seq.equiv_lim CauSeq.equiv_lim

theorem eq_lim_of_const_equiv {f : CauSeq Î² abv} {x : Î²} (h : CauSeq.const abv x â‰ˆ f) : x = lim f :=
  const_equiv.mp <| Setoid.trans h <| equiv_lim f
#align cau_seq.eq_lim_of_const_equiv CauSeq.eq_lim_of_const_equiv

theorem lim_eq_of_equiv_const {f : CauSeq Î² abv} {x : Î²} (h : f â‰ˆ CauSeq.const abv x) : lim f = x :=
  (eq_lim_of_const_equiv <| Setoid.symm h).symm
#align cau_seq.lim_eq_of_equiv_const CauSeq.lim_eq_of_equiv_const

theorem lim_eq_lim_of_equiv {f g : CauSeq Î² abv} (h : f â‰ˆ g) : lim f = lim g :=
  lim_eq_of_equiv_const <| Setoid.trans h <| equiv_lim g
#align cau_seq.lim_eq_lim_of_equiv CauSeq.lim_eq_lim_of_equiv

@[simp]
theorem lim_const (x : Î²) : lim (const abv x) = x :=
  lim_eq_of_equiv_const <| Setoid.refl _
#align cau_seq.lim_const CauSeq.lim_const

theorem lim_add (f g : CauSeq Î² abv) : lim f + lim g = lim (f + g) :=
  eq_lim_of_const_equiv <|
    show LimZero (const abv (lim f + lim g) - (f + g)) by
      rw [const_add, add_sub_add_comm]
      -- âŠ¢ LimZero (const abv (lim f) - f + (const abv (lim g) - g))
      exact add_limZero (Setoid.symm (equiv_lim f)) (Setoid.symm (equiv_lim g))
      -- ğŸ‰ no goals
#align cau_seq.lim_add CauSeq.lim_add

theorem lim_mul_lim (f g : CauSeq Î² abv) : lim f * lim g = lim (f * g) :=
  eq_lim_of_const_equiv <|
    show LimZero (const abv (lim f * lim g) - f * g) by
      have h :
        const abv (lim f * lim g) - f * g =
          (const abv (lim f) - f) * g + const abv (lim f) * (const abv (lim g) - g) := by
              apply Subtype.ext
              rw [coe_add]
              simp [sub_mul, mul_sub]
      rw [h]
      -- âŠ¢ LimZero ((const abv (lim f) - f) * g + const abv (lim f) * (const abv (lim g â€¦
      exact
        add_limZero (mul_limZero_left _ (Setoid.symm (equiv_lim _)))
          (mul_limZero_right _ (Setoid.symm (equiv_lim _)))
#align cau_seq.lim_mul_lim CauSeq.lim_mul_lim

theorem lim_mul (f : CauSeq Î² abv) (x : Î²) : lim f * x = lim (f * const abv x) := by
  rw [â† lim_mul_lim, lim_const]
  -- ğŸ‰ no goals
#align cau_seq.lim_mul CauSeq.lim_mul

theorem lim_neg (f : CauSeq Î² abv) : lim (-f) = -lim f :=
  lim_eq_of_equiv_const
    (show LimZero (-f - const abv (-lim f)) by
      rw [const_neg, sub_neg_eq_add, add_comm, â† sub_eq_add_neg]
      -- âŠ¢ LimZero (const abv (lim f) - f)
      exact Setoid.symm (equiv_lim f))
      -- ğŸ‰ no goals
#align cau_seq.lim_neg CauSeq.lim_neg

theorem lim_eq_zero_iff (f : CauSeq Î² abv) : lim f = 0 â†” LimZero f :=
  âŸ¨fun h => by
    have hf := equiv_lim f
    -- âŠ¢ LimZero f
    rw [h] at hf
    -- âŠ¢ LimZero f
    exact (limZero_congr hf).mpr (const_limZero.mpr rfl),
    -- ğŸ‰ no goals
   fun h => by
    have hâ‚ : f = f - const abv 0 := ext fun n => by simp [sub_apply, const_apply]
    -- âŠ¢ lim f = 0
    rw [hâ‚] at h
    -- âŠ¢ lim f = 0
    exact lim_eq_of_equiv_const hâŸ©
    -- ğŸ‰ no goals
#align cau_seq.lim_eq_zero_iff CauSeq.lim_eq_zero_iff

end

section

variable {Î² : Type*} [Field Î²] {abv : Î² â†’ Î±} [IsAbsoluteValue abv] [IsComplete Î² abv]

theorem lim_inv {f : CauSeq Î² abv} (hf : Â¬LimZero f) : lim (inv f hf) = (lim f)â»Â¹ :=
  have hl : lim f â‰  0 := by rwa [â† lim_eq_zero_iff] at hf
                            -- ğŸ‰ no goals
  lim_eq_of_equiv_const <|
    show LimZero (inv f hf - const abv (lim f)â»Â¹) from
      have hâ‚ : âˆ€ (g f : CauSeq Î² abv) (hf : Â¬LimZero f), LimZero (g - f * inv f hf * g) :=
        fun g f hf => by
          have hâ‚‚ : g - f * inv f hf * g = 1 * g - f * inv f hf * g := by rw [one_mul g]
          -- âŠ¢ LimZero (g - f * inv f hf * g)
          have hâ‚ƒ : f * inv f hf * g = (f * inv f hf) * g := by simp [mul_assoc]
          -- âŠ¢ LimZero (g - f * inv f hf * g)
          have hâ‚„ : g - f * inv f hf * g = (1 - f * inv f hf) * g := by rw [hâ‚‚, hâ‚ƒ, â† sub_mul]
          -- âŠ¢ LimZero (g - f * inv f hf * g)
          have hâ‚… : g - f * inv f hf * g = g * (1 - f * inv f hf) := by rw [hâ‚„, mul_comm]
          -- âŠ¢ LimZero (g - f * inv f hf * g)
          have hâ‚† : g - f * inv f hf * g = g * (1 - inv f hf * f) := by rw [hâ‚…, mul_comm f]
          -- âŠ¢ LimZero (g - f * inv f hf * g)
          rw [hâ‚†]; exact mul_limZero_right _ (Setoid.symm (CauSeq.inv_mul_cancel _))
          -- âŠ¢ LimZero (g * (1 - inv f hf * f))
                   -- ğŸ‰ no goals
      have hâ‚‚ :
        LimZero
          (inv f hf - const abv (lim f)â»Â¹ -
            (const abv (lim f) - f) * (inv f hf * const abv (lim f)â»Â¹)) := by
              rw [sub_mul, â† sub_add, sub_sub, sub_add_eq_sub_sub, sub_right_comm, sub_add]
              -- âŠ¢ LimZero (inv f hf - const abv (lim f) * (inv f hf * const abv (lim f)â»Â¹) - ( â€¦
              show LimZero
                (inv f hf - const abv (lim f) * (inv f hf * const abv (lim f)â»Â¹) -
                  (const abv (lim f)â»Â¹ - f * (inv f hf * const abv (lim f)â»Â¹)))
              exact sub_limZero
                (by rw [â† mul_assoc, mul_right_comm, const_inv hl]; exact hâ‚ _ _ _)
                (by rw [â† mul_assoc]; exact hâ‚ _ _ _)
      (limZero_congr hâ‚‚).mpr <| mul_limZero_left _ (Setoid.symm (equiv_lim f))
#align cau_seq.lim_inv CauSeq.lim_inv

end

section

variable [IsComplete Î± abs]

theorem lim_le {f : CauSeq Î± abs} {x : Î±} (h : f â‰¤ CauSeq.const abs x) : lim f â‰¤ x :=
  CauSeq.const_le.1 <| CauSeq.le_of_eq_of_le (Setoid.symm (equiv_lim f)) h
#align cau_seq.lim_le CauSeq.lim_le

theorem le_lim {f : CauSeq Î± abs} {x : Î±} (h : CauSeq.const abs x â‰¤ f) : x â‰¤ lim f :=
  CauSeq.const_le.1 <| CauSeq.le_of_le_of_eq h (equiv_lim f)
#align cau_seq.le_lim CauSeq.le_lim

theorem lt_lim {f : CauSeq Î± abs} {x : Î±} (h : CauSeq.const abs x < f) : x < lim f :=
  CauSeq.const_lt.1 <| CauSeq.lt_of_lt_of_eq h (equiv_lim f)
#align cau_seq.lt_lim CauSeq.lt_lim

theorem lim_lt {f : CauSeq Î± abs} {x : Î±} (h : f < CauSeq.const abs x) : lim f < x :=
  CauSeq.const_lt.1 <| CauSeq.lt_of_eq_of_lt (Setoid.symm (equiv_lim f)) h
#align cau_seq.lim_lt CauSeq.lim_lt

end

end CauSeq
