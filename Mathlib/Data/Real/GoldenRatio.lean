/-
Copyright (c) 2020 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker, Alexey Soloyev, Junyan Xu
-/
import Mathlib.Data.Real.Irrational
import Mathlib.Data.Nat.Fib
import Mathlib.Data.Nat.PrimeNormNum
import Mathlib.Data.Fin.VecNotation
import Mathlib.Algebra.LinearRecurrence
import Mathlib.Tactic.NormNum.NatFib

#align_import data.real.golden_ratio from "leanprover-community/mathlib"@"2196ab363eb097c008d4497125e0dde23fb36db2"

/-!
# The golden ratio and its conjugate

This file defines the golden ratio `Ï† := (1 + âˆš5)/2` and its conjugate
`Ïˆ := (1 - âˆš5)/2`, which are the two real roots of `XÂ² - X - 1`.

Along with various computational facts about them, we prove their
irrationality, and we link them to the Fibonacci sequence by proving
Binet's formula.
-/


noncomputable section

open Polynomial

/-- The golden ratio `Ï† := (1 + âˆš5)/2`. -/
@[reducible]
def goldenRatio :=
  (1 + Real.sqrt 5) / 2
#align golden_ratio goldenRatio

/-- The conjugate of the golden ratio `Ïˆ := (1 - âˆš5)/2`. -/
@[reducible]
def goldenConj :=
  (1 - Real.sqrt 5) / 2
#align golden_conj goldenConj

@[inherit_doc goldenRatio] scoped[Real] notation "Ï†" => goldenRatio
@[inherit_doc goldenConj] scoped[Real] notation "Ïˆ" => goldenConj
open Real

/-- The inverse of the golden ratio is the opposite of its conjugate. -/
theorem inv_gold : Ï†â»Â¹ = -Ïˆ := by
  have : 1 + Real.sqrt 5 â‰  0 := ne_of_gt (add_pos (by norm_num) <| Real.sqrt_pos.mpr (by norm_num))
  -- âŠ¢ Ï†â»Â¹ = -Ïˆ
  field_simp [sub_mul, mul_add]
  -- âŠ¢ 2 * 2 = 5 - 1
  norm_num
  -- ğŸ‰ no goals
#align inv_gold inv_gold

/-- The opposite of the golden ratio is the inverse of its conjugate. -/
theorem inv_goldConj : Ïˆâ»Â¹ = -Ï† := by
  rw [inv_eq_iff_eq_inv, â† neg_inv, â† neg_eq_iff_eq_neg]
  -- âŠ¢ -Ïˆ = Ï†â»Â¹
  exact inv_gold.symm
  -- ğŸ‰ no goals
#align inv_gold_conj inv_goldConj

@[simp]
theorem gold_mul_goldConj : Ï† * Ïˆ = -1 := by
  field_simp
  -- âŠ¢ (1 + sqrt 5) * (1 - sqrt 5) = -(2 * 2)
  rw [â† sq_sub_sq]
  -- âŠ¢ 1 ^ 2 - sqrt 5 ^ 2 = -(2 * 2)
  norm_num
  -- ğŸ‰ no goals
#align gold_mul_gold_conj gold_mul_goldConj

@[simp]
theorem goldConj_mul_gold : Ïˆ * Ï† = -1 := by
  rw [mul_comm]
  -- âŠ¢ Ï† * Ïˆ = -1
  exact gold_mul_goldConj
  -- ğŸ‰ no goals
#align gold_conj_mul_gold goldConj_mul_gold

@[simp]
theorem gold_add_goldConj : Ï† + Ïˆ = 1 := by
  rw [goldenRatio, goldenConj]
  -- âŠ¢ (1 + sqrt 5) / 2 + (1 - sqrt 5) / 2 = 1
  ring
  -- ğŸ‰ no goals
#align gold_add_gold_conj gold_add_goldConj

theorem one_sub_goldConj : 1 - Ï† = Ïˆ := by
  linarith [gold_add_goldConj]
  -- ğŸ‰ no goals
#align one_sub_gold_conj one_sub_goldConj

theorem one_sub_gold : 1 - Ïˆ = Ï† := by
  linarith [gold_add_goldConj]
  -- ğŸ‰ no goals
#align one_sub_gold one_sub_gold

@[simp]
theorem gold_sub_goldConj : Ï† - Ïˆ = Real.sqrt 5 := by
  rw [goldenRatio, goldenConj]
  -- âŠ¢ (1 + sqrt 5) / 2 - (1 - sqrt 5) / 2 = sqrt 5
  ring
  -- ğŸ‰ no goals
#align gold_sub_gold_conj gold_sub_goldConj

@[simp 1200]
theorem gold_sq : Ï† ^ 2 = Ï† + 1 := by
  rw [goldenRatio, â† sub_eq_zero]
  -- âŠ¢ ((1 + sqrt 5) / 2) ^ 2 - ((1 + sqrt 5) / 2 + 1) = 0
  ring_nf
  -- âŠ¢ sqrt 5 ^ 2 * (â†‘(Int.ofNat 1) / â†‘4) + â†‘(Int.negOfNat 5) * (â†‘(Int.ofNat 1) / â†‘ â€¦
  rw [Real.sq_sqrt] <;> norm_num
                        -- ğŸ‰ no goals
                        -- ğŸ‰ no goals
#align gold_sq gold_sq

@[simp 1200]
theorem goldConj_sq : Ïˆ ^ 2 = Ïˆ + 1 := by
  rw [goldenConj, â† sub_eq_zero]
  -- âŠ¢ ((1 - sqrt 5) / 2) ^ 2 - ((1 - sqrt 5) / 2 + 1) = 0
  ring_nf
  -- âŠ¢ sqrt 5 ^ 2 * (â†‘(Int.ofNat 1) / â†‘4) + â†‘(Int.negOfNat 5) * (â†‘(Int.ofNat 1) / â†‘ â€¦
  rw [Real.sq_sqrt] <;> norm_num
                        -- ğŸ‰ no goals
                        -- ğŸ‰ no goals
#align gold_conj_sq goldConj_sq

theorem gold_pos : 0 < Ï† :=
  mul_pos (by apply add_pos <;> norm_num) <| inv_pos.2 zero_lt_two
              -- âŠ¢ 0 < 1
                                -- ğŸ‰ no goals
                                -- ğŸ‰ no goals
#align gold_pos gold_pos

theorem gold_ne_zero : Ï† â‰  0 :=
  ne_of_gt gold_pos
#align gold_ne_zero gold_ne_zero

theorem one_lt_gold : 1 < Ï† := by
  refine' lt_of_mul_lt_mul_left _ (le_of_lt gold_pos)
  -- âŠ¢ Ï† * 1 < Ï† * Ï†
  simp [â† sq, gold_pos, zero_lt_one, - div_pow] -- Porting note: Added `- div_pow`
  -- ğŸ‰ no goals
#align one_lt_gold one_lt_gold

theorem goldConj_neg : Ïˆ < 0 := by
  linarith [one_sub_goldConj, one_lt_gold]
  -- ğŸ‰ no goals
#align gold_conj_neg goldConj_neg

theorem goldConj_ne_zero : Ïˆ â‰  0 :=
  ne_of_lt goldConj_neg
#align gold_conj_ne_zero goldConj_ne_zero

theorem neg_one_lt_goldConj : -1 < Ïˆ := by
  rw [neg_lt, â† inv_gold]
  -- âŠ¢ Ï†â»Â¹ < 1
  exact inv_lt_one one_lt_gold
  -- ğŸ‰ no goals
#align neg_one_lt_gold_conj neg_one_lt_goldConj

/-!
## Irrationality
-/


/-- The golden ratio is irrational. -/
theorem gold_irrational : Irrational Ï† := by
  have := Nat.Prime.irrational_sqrt (show Nat.Prime 5 by norm_num)
  -- âŠ¢ Irrational Ï†
  have := this.rat_add 1
  -- âŠ¢ Irrational Ï†
  have := this.rat_mul (show (0.5 : â„š) â‰  0 by norm_num)
  -- âŠ¢ Irrational Ï†
  convert this
  -- âŠ¢ Ï† = â†‘0.5 * (â†‘1 + sqrt â†‘5)
  norm_num
  -- âŠ¢ Ï† = 1 / 2 * (1 + sqrt 5)
  field_simp
  -- ğŸ‰ no goals
#align gold_irrational gold_irrational

/-- The conjugate of the golden ratio is irrational. -/
theorem goldConj_irrational : Irrational Ïˆ := by
  have := Nat.Prime.irrational_sqrt (show Nat.Prime 5 by norm_num)
  -- âŠ¢ Irrational Ïˆ
  have := this.rat_sub 1
  -- âŠ¢ Irrational Ïˆ
  have := this.rat_mul (show (0.5 : â„š) â‰  0 by norm_num)
  -- âŠ¢ Irrational Ïˆ
  convert this
  -- âŠ¢ Ïˆ = â†‘0.5 * (â†‘1 - sqrt â†‘5)
  norm_num
  -- âŠ¢ Ïˆ = 1 / 2 * (1 - sqrt 5)
  field_simp
  -- ğŸ‰ no goals
#align gold_conj_irrational goldConj_irrational

/-!
## Links with Fibonacci sequence
-/


section Fibrec

variable {Î± : Type*} [CommSemiring Î±]

/-- The recurrence relation satisfied by the Fibonacci sequence. -/
def fibRec : LinearRecurrence Î± where
  order := 2
  coeffs := ![1, 1]
#align fib_rec fibRec

section Poly

open Polynomial

/-- The characteristic polynomial of `fibRec` is `XÂ² - (X + 1)`. -/
theorem fibRec_charPoly_eq {Î² : Type*} [CommRing Î²] :
    fibRec.charPoly = X ^ 2 - (X + (1 : Î²[X])) := by
  rw [fibRec, LinearRecurrence.charPoly]
  -- âŠ¢ (â†‘(monomial { order := 2, coeffs := ![1, 1] }.order) 1 - Finset.sum Finset.u â€¦
  simp [Finset.sum_fin_eq_sum_range, Finset.sum_range_succ', â† smul_X_eq_monomial]
  -- ğŸ‰ no goals
#align fib_rec_char_poly_eq fibRec_charPoly_eq

end Poly

/-- As expected, the Fibonacci sequence is a solution of `fibRec`. -/
theorem fib_isSol_fibRec : fibRec.IsSolution (fun x => x.fib : â„• â†’ Î±) := by
  rw [fibRec]
  -- âŠ¢ LinearRecurrence.IsSolution { order := 2, coeffs := ![1, 1] } fun x => â†‘(Nat â€¦
  intro n
  -- âŠ¢ (fun x => â†‘(Nat.fib x)) (n + { order := 2, coeffs := ![1, 1] }.order) = Fins â€¦
  simp only
  -- âŠ¢ â†‘(Nat.fib (n + 2)) = Finset.sum Finset.univ fun x => Matrix.vecCons 1 ![1] x â€¦
  rw [Nat.fib_add_two, add_comm]
  -- âŠ¢ â†‘(Nat.fib (n + 1) + Nat.fib n) = Finset.sum Finset.univ fun x => Matrix.vecC â€¦
  simp [Finset.sum_fin_eq_sum_range, Finset.sum_range_succ']
  -- ğŸ‰ no goals
#align fib_is_sol_fib_rec fib_isSol_fibRec

/-- The geometric sequence `fun n â†¦ Ï†^n` is a solution of `fibRec`. -/
theorem geom_gold_isSol_fibRec : fibRec.IsSolution (Ï† ^ Â·) := by
  rw [fibRec.geom_sol_iff_root_charPoly, fibRec_charPoly_eq]
  -- âŠ¢ IsRoot (X ^ 2 - (X + 1)) Ï†
  simp [sub_eq_zero, - div_pow] -- Porting note: Added `- div_pow`
  -- ğŸ‰ no goals
#align geom_gold_is_sol_fib_rec geom_gold_isSol_fibRec

/-- The geometric sequence `fun n â†¦ Ïˆ^n` is a solution of `fibRec`. -/
theorem geom_goldConj_isSol_fibRec : fibRec.IsSolution (Ïˆ ^ Â·) := by
  rw [fibRec.geom_sol_iff_root_charPoly, fibRec_charPoly_eq]
  -- âŠ¢ IsRoot (X ^ 2 - (X + 1)) Ïˆ
  simp [sub_eq_zero, - div_pow] -- Porting note: Added `- div_pow`
  -- ğŸ‰ no goals
#align geom_gold_conj_is_sol_fib_rec geom_goldConj_isSol_fibRec

end Fibrec

/-- Binet's formula as a function equality. -/
theorem Real.coe_fib_eq' :
    (fun n => Nat.fib n : â„• â†’ â„) = fun n => (Ï† ^ n - Ïˆ ^ n) / Real.sqrt 5 := by
  rw [fibRec.sol_eq_of_eq_init]
  Â· intro i hi
    -- âŠ¢ (fun n => â†‘(Nat.fib n)) i = (fun n => (Ï† ^ n - Ïˆ ^ n) / sqrt 5) i
    norm_cast at hi
    -- âŠ¢ (fun n => â†‘(Nat.fib n)) i = (fun n => (Ï† ^ n - Ïˆ ^ n) / sqrt 5) i
    fin_cases hi
    -- âŠ¢ (fun n => â†‘(Nat.fib n)) 0 = (fun n => (Ï† ^ n - Ïˆ ^ n) / sqrt 5) 0
    Â· simp
      -- ğŸ‰ no goals
    Â· simp only [goldenRatio, goldenConj]
      -- âŠ¢ â†‘(Nat.fib 1) = (((1 + sqrt 5) / 2) ^ 1 - ((1 - sqrt 5) / 2) ^ 1) / sqrt 5
      ring_nf
      -- âŠ¢ 1 = sqrt 5 * (sqrt 5)â»Â¹
      rw [mul_inv_cancel]; norm_num
      -- âŠ¢ sqrt 5 â‰  0
                           -- ğŸ‰ no goals
  Â· exact fib_isSol_fibRec
    -- ğŸ‰ no goals
  Â· -- Porting note: Rewrote this proof
    suffices LinearRecurrence.IsSolution fibRec
        ((fun n â†¦ (sqrt 5)â»Â¹ * Ï† ^ n) - (fun n â†¦ (sqrt 5)â»Â¹ * Ïˆ ^ n)) by
      convert this
      rw [Pi.sub_apply]
      ring
    apply (@fibRec â„ _).solSpace.sub_mem
    -- âŠ¢ (fun n => (sqrt 5)â»Â¹ * Ï† ^ n) âˆˆ LinearRecurrence.solSpace fibRec
    Â· exact Submodule.smul_mem fibRec.solSpace (Real.sqrt 5)â»Â¹ geom_gold_isSol_fibRec
      -- ğŸ‰ no goals
    Â· exact Submodule.smul_mem fibRec.solSpace (Real.sqrt 5)â»Â¹ geom_goldConj_isSol_fibRec
      -- ğŸ‰ no goals
#align real.coe_fib_eq' Real.coe_fib_eq'

/-- Binet's formula as a dependent equality. -/
theorem Real.coe_fib_eq : âˆ€ n, (Nat.fib n : â„) = (Ï† ^ n - Ïˆ ^ n) / Real.sqrt 5 := by
  rw [â† Function.funext_iff, Real.coe_fib_eq']
  -- ğŸ‰ no goals
#align real.coe_fib_eq Real.coe_fib_eq
