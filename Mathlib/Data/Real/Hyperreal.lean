/-
Copyright (c) 2019 Abhimanyu Pallavi Sudhir. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Abhimanyu Pallavi Sudhir
-/
import Mathlib.Order.Filter.FilterProduct
import Mathlib.Analysis.SpecificLimits.Basic

#align_import data.real.hyperreal from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Construction of the hyperreal numbers as an ultraproduct of real sequences.
-/


open scoped Classical
open Filter Germ Topology

/-- Hyperreal numbers on the ultrafilter extending the cofinite filter -/
def Hyperreal : Type :=
  Germ (hyperfilter ‚Ñï : Filter ‚Ñï) ‚Ñù deriving Inhabited
#align hyperreal Hyperreal

namespace Hyperreal

@[inherit_doc] notation "‚Ñù*" => Hyperreal

noncomputable instance : LinearOrderedField ‚Ñù* :=
  inferInstanceAs (LinearOrderedField (Germ _ _))

/-- Natural embedding `‚Ñù ‚Üí ‚Ñù*`. -/
@[coe] def ofReal : ‚Ñù ‚Üí ‚Ñù* := const

noncomputable instance : CoeTC ‚Ñù ‚Ñù* := ‚ü®ofReal‚ü©

@[simp, norm_cast]
theorem coe_eq_coe {x y : ‚Ñù} : (x : ‚Ñù*) = y ‚Üî x = y :=
  Germ.const_inj
#align hyperreal.coe_eq_coe Hyperreal.coe_eq_coe

theorem coe_ne_coe {x y : ‚Ñù} : (x : ‚Ñù*) ‚â† y ‚Üî x ‚â† y :=
  coe_eq_coe.not
#align hyperreal.coe_ne_coe Hyperreal.coe_ne_coe

@[simp, norm_cast]
theorem coe_eq_zero {x : ‚Ñù} : (x : ‚Ñù*) = 0 ‚Üî x = 0 :=
  coe_eq_coe
#align hyperreal.coe_eq_zero Hyperreal.coe_eq_zero

@[simp, norm_cast]
theorem coe_eq_one {x : ‚Ñù} : (x : ‚Ñù*) = 1 ‚Üî x = 1 :=
  coe_eq_coe
#align hyperreal.coe_eq_one Hyperreal.coe_eq_one

@[norm_cast]
theorem coe_ne_zero {x : ‚Ñù} : (x : ‚Ñù*) ‚â† 0 ‚Üî x ‚â† 0 :=
  coe_ne_coe
#align hyperreal.coe_ne_zero Hyperreal.coe_ne_zero

@[norm_cast]
theorem coe_ne_one {x : ‚Ñù} : (x : ‚Ñù*) ‚â† 1 ‚Üî x ‚â† 1 :=
  coe_ne_coe
#align hyperreal.coe_ne_one Hyperreal.coe_ne_one

@[simp, norm_cast]
theorem coe_one : ‚Üë(1 : ‚Ñù) = (1 : ‚Ñù*) :=
  rfl
#align hyperreal.coe_one Hyperreal.coe_one

@[simp, norm_cast]
theorem coe_zero : ‚Üë(0 : ‚Ñù) = (0 : ‚Ñù*) :=
  rfl
#align hyperreal.coe_zero Hyperreal.coe_zero

@[simp, norm_cast]
theorem coe_inv (x : ‚Ñù) : ‚Üëx‚Åª¬π = (x‚Åª¬π : ‚Ñù*) :=
  rfl
#align hyperreal.coe_inv Hyperreal.coe_inv

@[simp, norm_cast]
theorem coe_neg (x : ‚Ñù) : ‚Üë(-x) = (-x : ‚Ñù*) :=
  rfl
#align hyperreal.coe_neg Hyperreal.coe_neg

@[simp, norm_cast]
theorem coe_add (x y : ‚Ñù) : ‚Üë(x + y) = (x + y : ‚Ñù*) :=
  rfl
#align hyperreal.coe_add Hyperreal.coe_add

#noalign hyperreal.coe_bit0
#noalign hyperreal.coe_bit1

-- See note [no_index around OfNat.ofNat]
@[simp, norm_cast]
theorem coe_ofNat (n : ‚Ñï) [n.AtLeastTwo] :
    ((no_index (OfNat.ofNat n : ‚Ñù)) : ‚Ñù*) = OfNat.ofNat n :=
  rfl

@[simp, norm_cast]
theorem coe_mul (x y : ‚Ñù) : ‚Üë(x * y) = (x * y : ‚Ñù*) :=
  rfl
#align hyperreal.coe_mul Hyperreal.coe_mul

@[simp, norm_cast]
theorem coe_div (x y : ‚Ñù) : ‚Üë(x / y) = (x / y : ‚Ñù*) :=
  rfl
#align hyperreal.coe_div Hyperreal.coe_div

@[simp, norm_cast]
theorem coe_sub (x y : ‚Ñù) : ‚Üë(x - y) = (x - y : ‚Ñù*) :=
  rfl
#align hyperreal.coe_sub Hyperreal.coe_sub

@[simp, norm_cast]
theorem coe_le_coe {x y : ‚Ñù} : (x : ‚Ñù*) ‚â§ y ‚Üî x ‚â§ y :=
  Germ.const_le_iff
#align hyperreal.coe_le_coe Hyperreal.coe_le_coe

@[simp, norm_cast]
theorem coe_lt_coe {x y : ‚Ñù} : (x : ‚Ñù*) < y ‚Üî x < y :=
  Germ.const_lt_iff
#align hyperreal.coe_lt_coe Hyperreal.coe_lt_coe

@[simp, norm_cast]
theorem coe_nonneg {x : ‚Ñù} : 0 ‚â§ (x : ‚Ñù*) ‚Üî 0 ‚â§ x :=
  coe_le_coe
#align hyperreal.coe_nonneg Hyperreal.coe_nonneg

@[simp, norm_cast]
theorem coe_pos {x : ‚Ñù} : 0 < (x : ‚Ñù*) ‚Üî 0 < x :=
  coe_lt_coe
#align hyperreal.coe_pos Hyperreal.coe_pos

@[simp, norm_cast]
theorem coe_abs (x : ‚Ñù) : ((|x| : ‚Ñù) : ‚Ñù*) = |‚Üëx| :=
  const_abs x
#align hyperreal.coe_abs Hyperreal.coe_abs

@[simp, norm_cast]
theorem coe_max (x y : ‚Ñù) : ((max x y : ‚Ñù) : ‚Ñù*) = max ‚Üëx ‚Üëy :=
  Germ.const_max _ _
#align hyperreal.coe_max Hyperreal.coe_max

@[simp, norm_cast]
theorem coe_min (x y : ‚Ñù) : ((min x y : ‚Ñù) : ‚Ñù*) = min ‚Üëx ‚Üëy :=
  Germ.const_min _ _
#align hyperreal.coe_min Hyperreal.coe_min

/-- Construct a hyperreal number from a sequence of real numbers. -/
def ofSeq (f : ‚Ñï ‚Üí ‚Ñù) : ‚Ñù* := (‚Üëf : Germ (hyperfilter ‚Ñï : Filter ‚Ñï) ‚Ñù)
#align hyperreal.of_seq Hyperreal.ofSeq

theorem ofSeq_surjective : Function.Surjective ofSeq := Quot.exists_rep

theorem ofSeq_lt_ofSeq {f g : ‚Ñï ‚Üí ‚Ñù} : ofSeq f < ofSeq g ‚Üî ‚àÄ·∂† n in hyperfilter ‚Ñï, f n < g n :=
  Germ.coe_lt

/-- A sample infinitesimal hyperreal-/
noncomputable def epsilon : ‚Ñù* :=
  ofSeq fun n => n‚Åª¬π
#align hyperreal.epsilon Hyperreal.epsilon

/-- A sample infinite hyperreal-/
noncomputable def omega : ‚Ñù* := ofSeq Nat.cast
#align hyperreal.omega Hyperreal.omega

@[inherit_doc] scoped notation "Œµ" => Hyperreal.epsilon
@[inherit_doc] scoped notation "œâ" => Hyperreal.omega

@[simp]
theorem inv_omega : œâ‚Åª¬π = Œµ :=
  rfl
#align hyperreal.inv_omega Hyperreal.inv_omega

@[simp]
theorem inv_epsilon : Œµ‚Åª¬π = œâ :=
  @inv_inv _ _ œâ
#align hyperreal.inv_epsilon Hyperreal.inv_epsilon

theorem omega_pos : 0 < œâ :=
  Germ.coe_pos.2 <| Nat.hyperfilter_le_atTop <| (eventually_gt_atTop 0).mono fun _ ‚Ü¶
    Nat.cast_pos.2
#align hyperreal.omega_pos Hyperreal.omega_pos

theorem epsilon_pos : 0 < Œµ :=
  inv_pos_of_pos omega_pos
#align hyperreal.epsilon_pos Hyperreal.epsilon_pos

theorem epsilon_ne_zero : Œµ ‚â† 0 :=
  epsilon_pos.ne'
#align hyperreal.epsilon_ne_zero Hyperreal.epsilon_ne_zero

theorem omega_ne_zero : œâ ‚â† 0 :=
  omega_pos.ne'
#align hyperreal.omega_ne_zero Hyperreal.omega_ne_zero

theorem epsilon_mul_omega : Œµ * œâ = 1 :=
  @inv_mul_cancel _ _ œâ omega_ne_zero
#align hyperreal.epsilon_mul_omega Hyperreal.epsilon_mul_omega

theorem lt_of_tendsto_zero_of_pos {f : ‚Ñï ‚Üí ‚Ñù} (hf : Tendsto f atTop (ùìù 0)) :
    ‚àÄ {r : ‚Ñù}, 0 < r ‚Üí ofSeq f < (r : ‚Ñù*) := fun hr ‚Ü¶
  ofSeq_lt_ofSeq.2 <| (hf.eventually <| gt_mem_nhds hr).filter_mono Nat.hyperfilter_le_atTop
#align hyperreal.lt_of_tendsto_zero_of_pos Hyperreal.lt_of_tendsto_zero_of_pos

theorem neg_lt_of_tendsto_zero_of_pos {f : ‚Ñï ‚Üí ‚Ñù} (hf : Tendsto f atTop (ùìù 0)) :
    ‚àÄ {r : ‚Ñù}, 0 < r ‚Üí (-r : ‚Ñù*) < ofSeq f := fun hr =>
  have hg := hf.neg
  neg_lt_of_neg_lt (by rw [neg_zero] at hg; exact lt_of_tendsto_zero_of_pos hg hr)
#align hyperreal.neg_lt_of_tendsto_zero_of_pos Hyperreal.neg_lt_of_tendsto_zero_of_pos

theorem gt_of_tendsto_zero_of_neg {f : ‚Ñï ‚Üí ‚Ñù} (hf : Tendsto f atTop (ùìù 0)) :
    ‚àÄ {r : ‚Ñù}, r < 0 ‚Üí (r : ‚Ñù*) < ofSeq f := fun {r} hr => by
  rw [‚Üê neg_neg r, coe_neg]; exact neg_lt_of_tendsto_zero_of_pos hf (neg_pos.mpr hr)
#align hyperreal.gt_of_tendsto_zero_of_neg Hyperreal.gt_of_tendsto_zero_of_neg

theorem epsilon_lt_pos (x : ‚Ñù) : 0 < x ‚Üí Œµ < x :=
  lt_of_tendsto_zero_of_pos tendsto_inverse_atTop_nhds_zero_nat
#align hyperreal.epsilon_lt_pos Hyperreal.epsilon_lt_pos

/-- Standard part predicate -/
def IsSt (x : ‚Ñù*) (r : ‚Ñù) :=
  ‚àÄ Œ¥ : ‚Ñù, 0 < Œ¥ ‚Üí (r - Œ¥ : ‚Ñù*) < x ‚àß x < r + Œ¥
#align hyperreal.is_st Hyperreal.IsSt

/-- Standard part function: like a "round" to ‚Ñù instead of ‚Ñ§ -/
noncomputable def st : ‚Ñù* ‚Üí ‚Ñù := fun x => if h : ‚àÉ r, IsSt x r then Classical.choose h else 0
#align hyperreal.st Hyperreal.st

/-- A hyperreal number is infinitesimal if its standard part is 0 -/
def Infinitesimal (x : ‚Ñù*) :=
  IsSt x 0
#align hyperreal.infinitesimal Hyperreal.Infinitesimal

/-- A hyperreal number is positive infinite if it is larger than all real numbers -/
def InfinitePos (x : ‚Ñù*) :=
  ‚àÄ r : ‚Ñù, ‚Üër < x
#align hyperreal.infinite_pos Hyperreal.InfinitePos

/-- A hyperreal number is negative infinite if it is smaller than all real numbers -/
def InfiniteNeg (x : ‚Ñù*) :=
  ‚àÄ r : ‚Ñù, x < r
#align hyperreal.infinite_neg Hyperreal.InfiniteNeg

/-- A hyperreal number is infinite if it is infinite positive or infinite negative -/
def Infinite (x : ‚Ñù*) :=
  InfinitePos x ‚à® InfiniteNeg x
#align hyperreal.infinite Hyperreal.Infinite

/-!
### Some facts about `st`
-/

theorem isSt_ofSeq_iff_tendsto {f : ‚Ñï ‚Üí ‚Ñù} {r : ‚Ñù} :
    IsSt (ofSeq f) r ‚Üî Tendsto f (hyperfilter ‚Ñï) (ùìù r) :=
  Iff.trans (forall‚ÇÇ_congr fun _ _ ‚Ü¶ (ofSeq_lt_ofSeq.and ofSeq_lt_ofSeq).trans eventually_and.symm)
    (nhds_basis_Ioo_pos _).tendsto_right_iff.symm

theorem isSt_iff_tendsto {x : ‚Ñù*} {r : ‚Ñù} : IsSt x r ‚Üî x.Tendsto (ùìù r) := by
  rcases ofSeq_surjective x with ‚ü®f, rfl‚ü©
  exact isSt_ofSeq_iff_tendsto

theorem isSt_of_tendsto {f : ‚Ñï ‚Üí ‚Ñù} {r : ‚Ñù} (hf : Tendsto f atTop (ùìù r)) : IsSt (ofSeq f) r :=
  isSt_ofSeq_iff_tendsto.2 <| hf.mono_left Nat.hyperfilter_le_atTop
#align hyperreal.is_st_of_tendsto Hyperreal.isSt_of_tendsto

-- Porting note: moved up, renamed
protected theorem IsSt.lt {x y : ‚Ñù*} {r s : ‚Ñù} (hxr : IsSt x r) (hys : IsSt y s) (hrs : r < s) :
    x < y := by
  rcases ofSeq_surjective x with ‚ü®f, rfl‚ü©
  rcases ofSeq_surjective y with ‚ü®g, rfl‚ü©
  rw [isSt_ofSeq_iff_tendsto] at hxr hys
  exact ofSeq_lt_ofSeq.2 <| hxr.eventually_lt hys hrs
#align hyperreal.lt_of_is_st_lt Hyperreal.IsSt.lt

theorem IsSt.unique {x : ‚Ñù*} {r s : ‚Ñù} (hr : IsSt x r) (hs : IsSt x s) : r = s := by
  rcases ofSeq_surjective x with ‚ü®f, rfl‚ü©
  rw [isSt_ofSeq_iff_tendsto] at hr hs
  exact tendsto_nhds_unique hr hs
#align hyperreal.is_st_unique Hyperreal.IsSt.unique

theorem IsSt.st_eq {x : ‚Ñù*} {r : ‚Ñù} (hxr : IsSt x r) : st x = r := by
  have h : ‚àÉ r, IsSt x r := ‚ü®r, hxr‚ü©
  rw [st, dif_pos h]
  exact (Classical.choose_spec h).unique hxr
#align hyperreal.st_of_is_st Hyperreal.IsSt.st_eq

theorem IsSt.not_infinite {x : ‚Ñù*} {r : ‚Ñù} (h : IsSt x r) : ¬¨Infinite x := fun hi ‚Ü¶
  hi.elim (fun hp ‚Ü¶ lt_asymm (h 1 one_pos).2 (hp (r + 1))) fun hn ‚Ü¶
    lt_asymm (h 1 one_pos).1 (hn (r - 1))

theorem not_infinite_of_exists_st {x : ‚Ñù*} : (‚àÉ r : ‚Ñù, IsSt x r) ‚Üí ¬¨Infinite x := fun ‚ü®_r, hr‚ü© =>
  hr.not_infinite
#align hyperreal.not_infinite_of_exists_st Hyperreal.not_infinite_of_exists_st

theorem Infinite.st_eq {x : ‚Ñù*} (hi : Infinite x) : st x = 0 :=
  dif_neg fun ‚ü®_r, hr‚ü© ‚Ü¶ hr.not_infinite hi
#align hyperreal.st_infinite Hyperreal.Infinite.st_eq

theorem isSt_sSup {x : ‚Ñù*} (hni : ¬¨Infinite x) : IsSt x (sSup { y : ‚Ñù | (y : ‚Ñù*) < x }) :=
  let S : Set ‚Ñù := { y : ‚Ñù | (y : ‚Ñù*) < x }
  let R : ‚Ñù := sSup S
  let ‚ü®r‚ÇÅ, hr‚ÇÅ‚ü© := not_forall.mp (not_or.mp hni).2
  let ‚ü®r‚ÇÇ, hr‚ÇÇ‚ü© := not_forall.mp (not_or.mp hni).1
  have HR‚ÇÅ : S.Nonempty :=
    ‚ü®r‚ÇÅ - 1, lt_of_lt_of_le (coe_lt_coe.2 <| sub_one_lt _) (not_lt.mp hr‚ÇÅ)‚ü©
  have HR‚ÇÇ : BddAbove S :=
    ‚ü®r‚ÇÇ, fun _y hy => le_of_lt (coe_lt_coe.1 (lt_of_lt_of_le hy (not_lt.mp hr‚ÇÇ)))‚ü©
  fun Œ¥ hŒ¥ =>
  ‚ü®lt_of_not_le fun c =>
      have hc : ‚àÄ y ‚àà S, y ‚â§ R - Œ¥ := fun _y hy =>
        coe_le_coe.1 <| le_of_lt <| lt_of_lt_of_le hy c
      not_lt_of_le (csSup_le HR‚ÇÅ hc) <| sub_lt_self R hŒ¥,
    lt_of_not_le fun c =>
      have hc : ‚Üë(R + Œ¥ / 2) < x :=
        lt_of_lt_of_le (add_lt_add_left (coe_lt_coe.2 (half_lt_self hŒ¥)) R) c
      not_lt_of_le (le_csSup HR‚ÇÇ hc) <| (lt_add_iff_pos_right _).mpr <| half_pos hŒ¥‚ü©
#align hyperreal.is_st_Sup Hyperreal.isSt_sSup

theorem exists_st_of_not_infinite {x : ‚Ñù*} (hni : ¬¨Infinite x) : ‚àÉ r : ‚Ñù, IsSt x r :=
  ‚ü®sSup { y : ‚Ñù | (y : ‚Ñù*) < x }, isSt_sSup hni‚ü©
#align hyperreal.exists_st_of_not_infinite Hyperreal.exists_st_of_not_infinite

theorem st_eq_sSup {x : ‚Ñù*} : st x = sSup { y : ‚Ñù | (y : ‚Ñù*) < x } := by
  rcases _root_.em (Infinite x) with (hx|hx)
  ¬∑ rw [hx.st_eq]
    cases hx with
    | inl hx =>
      convert Real.sSup_univ.symm
      exact Set.eq_univ_of_forall hx
    | inr hx =>
      convert Real.sSup_empty.symm
      exact Set.eq_empty_of_forall_not_mem fun y hy ‚Ü¶ hy.out.not_lt (hx _)
  ¬∑ exact (isSt_sSup hx).st_eq
#align hyperreal.st_eq_Sup Hyperreal.st_eq_sSup

theorem exists_st_iff_not_infinite {x : ‚Ñù*} : (‚àÉ r : ‚Ñù, IsSt x r) ‚Üî ¬¨Infinite x :=
  ‚ü®not_infinite_of_exists_st, exists_st_of_not_infinite‚ü©
#align hyperreal.exists_st_iff_not_infinite Hyperreal.exists_st_iff_not_infinite

theorem infinite_iff_not_exists_st {x : ‚Ñù*} : Infinite x ‚Üî ¬¨‚àÉ r : ‚Ñù, IsSt x r :=
  iff_not_comm.mp exists_st_iff_not_infinite
#align hyperreal.infinite_iff_not_exists_st Hyperreal.infinite_iff_not_exists_st

theorem IsSt.isSt_st {x : ‚Ñù*} {r : ‚Ñù} (hxr : IsSt x r) : IsSt x (st x) := by
  rwa [hxr.st_eq]
#align hyperreal.is_st_st_of_is_st Hyperreal.IsSt.isSt_st

theorem isSt_st_of_exists_st {x : ‚Ñù*} (hx : ‚àÉ r : ‚Ñù, IsSt x r) : IsSt x (st x) :=
  let ‚ü®_r, hr‚ü© := hx; hr.isSt_st
#align hyperreal.is_st_st_of_exists_st Hyperreal.isSt_st_of_exists_st

theorem isSt_st' {x : ‚Ñù*} (hx : ¬¨Infinite x) : IsSt x (st x) :=
  (isSt_sSup hx).isSt_st
#align hyperreal.is_st_st' Hyperreal.isSt_st'

theorem isSt_st {x : ‚Ñù*} (hx : st x ‚â† 0) : IsSt x (st x) :=
  isSt_st' <| mt Infinite.st_eq hx
#align hyperreal.is_st_st Hyperreal.isSt_st

theorem isSt_refl_real (r : ‚Ñù) : IsSt r r := isSt_ofSeq_iff_tendsto.2 tendsto_const_nhds
#align hyperreal.is_st_refl_real Hyperreal.isSt_refl_real

theorem st_id_real (r : ‚Ñù) : st r = r := (isSt_refl_real r).st_eq
#align hyperreal.st_id_real Hyperreal.st_id_real

theorem eq_of_isSt_real {r s : ‚Ñù} : IsSt r s ‚Üí r = s :=
  (isSt_refl_real r).unique
#align hyperreal.eq_of_is_st_real Hyperreal.eq_of_isSt_real

theorem isSt_real_iff_eq {r s : ‚Ñù} : IsSt r s ‚Üî r = s :=
  ‚ü®eq_of_isSt_real, fun hrs => hrs ‚ñ∏ isSt_refl_real r‚ü©
#align hyperreal.is_st_real_iff_eq Hyperreal.isSt_real_iff_eq

theorem isSt_symm_real {r s : ‚Ñù} : IsSt r s ‚Üî IsSt s r := by
  rw [isSt_real_iff_eq, isSt_real_iff_eq, eq_comm]
#align hyperreal.is_st_symm_real Hyperreal.isSt_symm_real

theorem isSt_trans_real {r s t : ‚Ñù} : IsSt r s ‚Üí IsSt s t ‚Üí IsSt r t := by
  rw [isSt_real_iff_eq, isSt_real_iff_eq, isSt_real_iff_eq]; exact Eq.trans
#align hyperreal.is_st_trans_real Hyperreal.isSt_trans_real

theorem isSt_inj_real {r‚ÇÅ r‚ÇÇ s : ‚Ñù} (h1 : IsSt r‚ÇÅ s) (h2 : IsSt r‚ÇÇ s) : r‚ÇÅ = r‚ÇÇ :=
  Eq.trans (eq_of_isSt_real h1) (eq_of_isSt_real h2).symm
#align hyperreal.is_st_inj_real Hyperreal.isSt_inj_real

theorem isSt_iff_abs_sub_lt_delta {x : ‚Ñù*} {r : ‚Ñù} : IsSt x r ‚Üî ‚àÄ Œ¥ : ‚Ñù, 0 < Œ¥ ‚Üí |x - ‚Üër| < Œ¥ := by
  simp only [abs_sub_lt_iff, sub_lt_iff_lt_add, IsSt, and_comm, add_comm]
#align hyperreal.is_st_iff_abs_sub_lt_delta Hyperreal.isSt_iff_abs_sub_lt_delta

theorem IsSt.map {x : ‚Ñù*} {r : ‚Ñù} (hxr : IsSt x r) {f : ‚Ñù ‚Üí ‚Ñù} (hf : ContinuousAt f r) :
    IsSt (x.map f) (f r) := by
  rcases ofSeq_surjective x with ‚ü®g, rfl‚ü©
  exact isSt_ofSeq_iff_tendsto.2 <| hf.tendsto.comp (isSt_ofSeq_iff_tendsto.1 hxr)

theorem IsSt.map‚ÇÇ {x y : ‚Ñù*} {r s : ‚Ñù} (hxr : IsSt x r) (hys : IsSt y s) {f : ‚Ñù ‚Üí ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousAt (Function.uncurry f) (r, s)) : IsSt (x.map‚ÇÇ f y) (f r s) := by
  rcases ofSeq_surjective x with ‚ü®x, rfl‚ü©
  rcases ofSeq_surjective y with ‚ü®y, rfl‚ü©
  rw [isSt_ofSeq_iff_tendsto] at hxr hys
  exact isSt_ofSeq_iff_tendsto.2 <| hf.tendsto.comp (hxr.prod_mk_nhds hys)

theorem IsSt.add {x y : ‚Ñù*} {r s : ‚Ñù} (hxr : IsSt x r) (hys : IsSt y s) :
    IsSt (x + y) (r + s) := hxr.map‚ÇÇ hys continuous_add.continuousAt
#align hyperreal.is_st_add Hyperreal.IsSt.add

theorem IsSt.neg {x : ‚Ñù*} {r : ‚Ñù} (hxr : IsSt x r) : IsSt (-x) (-r) :=
  hxr.map continuous_neg.continuousAt
#align hyperreal.is_st_neg Hyperreal.IsSt.neg

theorem IsSt.sub {x y : ‚Ñù*} {r s : ‚Ñù} (hxr : IsSt x r) (hys : IsSt y s) : IsSt (x - y) (r - s) :=
  hxr.map‚ÇÇ hys continuous_sub.continuousAt
#align hyperreal.is_st_sub Hyperreal.IsSt.sub

theorem IsSt.le {x y : ‚Ñù*} {r s : ‚Ñù} (hrx : IsSt x r) (hsy : IsSt y s) (hxy : x ‚â§ y) : r ‚â§ s :=
  not_lt.1 fun h ‚Ü¶ hxy.not_lt <| hsy.lt hrx h
#align hyperreal.is_st_le_of_le Hyperreal.IsSt.le

theorem st_le_of_le {x y : ‚Ñù*} (hix : ¬¨Infinite x) (hiy : ¬¨Infinite y) : x ‚â§ y ‚Üí st x ‚â§ st y :=
  (isSt_st' hix).le (isSt_st' hiy)
#align hyperreal.st_le_of_le Hyperreal.st_le_of_le

theorem lt_of_st_lt {x y : ‚Ñù*} (hix : ¬¨Infinite x) (hiy : ¬¨Infinite y) : st x < st y ‚Üí x < y :=
  (isSt_st' hix).lt (isSt_st' hiy)
#align hyperreal.lt_of_st_lt Hyperreal.lt_of_st_lt

/-!
### Basic lemmas about infinite
-/

theorem infinitePos_def {x : ‚Ñù*} : InfinitePos x ‚Üî ‚àÄ r : ‚Ñù, ‚Üër < x := Iff.rfl
#align hyperreal.infinite_pos_def Hyperreal.infinitePos_def

theorem infiniteNeg_def {x : ‚Ñù*} : InfiniteNeg x ‚Üî ‚àÄ r : ‚Ñù, x < r := Iff.rfl
#align hyperreal.infinite_neg_def Hyperreal.infiniteNeg_def

theorem InfinitePos.pos {x : ‚Ñù*} (hip : InfinitePos x) : 0 < x := hip 0
#align hyperreal.pos_of_infinite_pos Hyperreal.InfinitePos.pos

theorem InfiniteNeg.lt_zero {x : ‚Ñù*} : InfiniteNeg x ‚Üí x < 0 := fun hin => hin 0
#align hyperreal.neg_of_infinite_neg Hyperreal.InfiniteNeg.lt_zero

theorem Infinite.ne_zero {x : ‚Ñù*} (hI : Infinite x) : x ‚â† 0 :=
  hI.elim (fun hip => hip.pos.ne') fun hin => hin.lt_zero.ne
#align hyperreal.ne_zero_of_infinite Hyperreal.Infinite.ne_zero

theorem not_infinite_zero : ¬¨Infinite 0 := fun hI => hI.ne_zero rfl
#align hyperreal.not_infinite_zero Hyperreal.not_infinite_zero

theorem InfiniteNeg.not_infinitePos {x : ‚Ñù*} : InfiniteNeg x ‚Üí ¬¨InfinitePos x := fun hn hp =>
  (hn 0).not_lt (hp 0)
#align hyperreal.not_infinite_pos_of_infinite_neg Hyperreal.InfiniteNeg.not_infinitePos

theorem InfinitePos.not_infiniteNeg {x : ‚Ñù*} (hp : InfinitePos x) : ¬¨InfiniteNeg x := fun hn ‚Ü¶
  hn.not_infinitePos hp
#align hyperreal.not_infinite_neg_of_infinite_pos Hyperreal.InfinitePos.not_infiniteNeg

theorem InfinitePos.neg {x : ‚Ñù*} : InfinitePos x ‚Üí InfiniteNeg (-x) := fun hp r =>
  neg_lt.mp (hp (-r))
#align hyperreal.infinite_neg_neg_of_infinite_pos Hyperreal.InfinitePos.neg

theorem InfiniteNeg.neg {x : ‚Ñù*} : InfiniteNeg x ‚Üí InfinitePos (-x) := fun hp r =>
  lt_neg.mp (hp (-r))
#align hyperreal.infinite_pos_neg_of_infinite_neg Hyperreal.InfiniteNeg.neg

-- Porting note: swapped LHS with RHS; added @[simp]
@[simp] theorem infiniteNeg_neg {x : ‚Ñù*} : InfiniteNeg (-x) ‚Üî InfinitePos x :=
  ‚ü®fun hin => neg_neg x ‚ñ∏ hin.neg, InfinitePos.neg‚ü©
#align hyperreal.infinite_pos_iff_infinite_neg_neg Hyperreal.infiniteNeg_neg‚Çì

-- Porting note: swapped LHS with RHS; added @[simp]
@[simp] theorem infinitePos_neg {x : ‚Ñù*} : InfinitePos (-x) ‚Üî InfiniteNeg x :=
  ‚ü®fun hin => neg_neg x ‚ñ∏ hin.neg, InfiniteNeg.neg‚ü©
#align hyperreal.infinite_neg_iff_infinite_pos_neg Hyperreal.infinitePos_neg‚Çì

-- Porting note: swapped LHS with RHS; added @[simp]
@[simp] theorem infinite_neg {x : ‚Ñù*} : Infinite (-x) ‚Üî Infinite x :=
  or_comm.trans <| infiniteNeg_neg.or infinitePos_neg
#align hyperreal.infinite_iff_infinite_neg Hyperreal.infinite_neg‚Çì

nonrec theorem Infinitesimal.not_infinite {x : ‚Ñù*} (h : Infinitesimal x) : ¬¨Infinite x :=
  h.not_infinite
#align hyperreal.not_infinite_of_infinitesimal Hyperreal.Infinitesimal.not_infinite

theorem Infinite.not_infinitesimal {x : ‚Ñù*} (h : Infinite x) : ¬¨Infinitesimal x := fun h' ‚Ü¶
  h'.not_infinite h
#align hyperreal.not_infinitesimal_of_infinite Hyperreal.Infinite.not_infinitesimal

theorem InfinitePos.not_infinitesimal {x : ‚Ñù*} (h : InfinitePos x) : ¬¨Infinitesimal x :=
  Infinite.not_infinitesimal (Or.inl h)
#align hyperreal.not_infinitesimal_of_infinite_pos Hyperreal.InfinitePos.not_infinitesimal

theorem InfiniteNeg.not_infinitesimal {x : ‚Ñù*} (h : InfiniteNeg x) : ¬¨Infinitesimal x :=
  Infinite.not_infinitesimal (Or.inr h)
#align hyperreal.not_infinitesimal_of_infinite_neg Hyperreal.InfiniteNeg.not_infinitesimal

theorem infinitePos_iff_infinite_and_pos {x : ‚Ñù*} : InfinitePos x ‚Üî Infinite x ‚àß 0 < x :=
  ‚ü®fun hip => ‚ü®Or.inl hip, hip 0‚ü©, fun ‚ü®hi, hp‚ü© =>
    hi.casesOn (fun hip => hip) fun hin => False.elim (not_lt_of_lt hp (hin 0))‚ü©
#align hyperreal.infinite_pos_iff_infinite_and_pos Hyperreal.infinitePos_iff_infinite_and_pos

theorem infiniteNeg_iff_infinite_and_neg {x : ‚Ñù*} : InfiniteNeg x ‚Üî Infinite x ‚àß x < 0 :=
  ‚ü®fun hip => ‚ü®Or.inr hip, hip 0‚ü©, fun ‚ü®hi, hp‚ü© =>
    hi.casesOn (fun hin => False.elim (not_lt_of_lt hp (hin 0))) fun hip => hip‚ü©
#align hyperreal.infinite_neg_iff_infinite_and_neg Hyperreal.infiniteNeg_iff_infinite_and_neg

theorem infinitePos_iff_infinite_of_nonneg {x : ‚Ñù*} (hp : 0 ‚â§ x) : InfinitePos x ‚Üî Infinite x :=
  .symm <| or_iff_left fun h ‚Ü¶ h.lt_zero.not_le hp
#align hyperreal.infinite_pos_iff_infinite_of_nonneg Hyperreal.infinitePos_iff_infinite_of_nonneg

theorem infinitePos_iff_infinite_of_pos {x : ‚Ñù*} (hp : 0 < x) : InfinitePos x ‚Üî Infinite x :=
  infinitePos_iff_infinite_of_nonneg hp.le
#align hyperreal.infinite_pos_iff_infinite_of_pos Hyperreal.infinitePos_iff_infinite_of_pos

theorem infiniteNeg_iff_infinite_of_neg {x : ‚Ñù*} (hn : x < 0) : InfiniteNeg x ‚Üî Infinite x :=
  .symm <| or_iff_right fun h ‚Ü¶ h.pos.not_lt hn
#align hyperreal.infinite_neg_iff_infinite_of_neg Hyperreal.infiniteNeg_iff_infinite_of_neg

theorem infinitePos_abs_iff_infinite_abs {x : ‚Ñù*} : InfinitePos |x| ‚Üî Infinite |x| :=
  infinitePos_iff_infinite_of_nonneg (abs_nonneg _)
#align hyperreal.infinite_pos_abs_iff_infinite_abs Hyperreal.infinitePos_abs_iff_infinite_abs

-- Porting note: swapped LHS with RHS; added @[simp]
@[simp] theorem infinite_abs_iff {x : ‚Ñù*} : Infinite |x| ‚Üî Infinite x := by
  cases le_total 0 x <;> simp [*, abs_of_nonneg, abs_of_nonpos, infinite_neg]
#align hyperreal.infinite_iff_infinite_abs Hyperreal.infinite_abs_iff‚Çì

-- Porting note: swapped LHS with RHS;
-- Porting note (#11215): TODO: make it a `simp` lemma
@[simp] theorem infinitePos_abs_iff_infinite {x : ‚Ñù*} : InfinitePos |x| ‚Üî Infinite x :=
  infinitePos_abs_iff_infinite_abs.trans infinite_abs_iff
#align hyperreal.infinite_iff_infinite_pos_abs Hyperreal.infinitePos_abs_iff_infinite‚Çì

theorem infinite_iff_abs_lt_abs {x : ‚Ñù*} : Infinite x ‚Üî ‚àÄ r : ‚Ñù, (|r| : ‚Ñù*) < |x| :=
  infinitePos_abs_iff_infinite.symm.trans ‚ü®fun hI r => coe_abs r ‚ñ∏ hI |r|, fun hR r =>
    (le_abs_self _).trans_lt (hR r)‚ü©
#align hyperreal.infinite_iff_abs_lt_abs Hyperreal.infinite_iff_abs_lt_abs

theorem infinitePos_add_not_infiniteNeg {x y : ‚Ñù*} :
    InfinitePos x ‚Üí ¬¨InfiniteNeg y ‚Üí InfinitePos (x + y) := by
  intro hip hnin r
  cases' not_forall.mp hnin with r‚ÇÇ hr‚ÇÇ
  convert add_lt_add_of_lt_of_le (hip (r + -r‚ÇÇ)) (not_lt.mp hr‚ÇÇ) using 1
  simp
#align hyperreal.infinite_pos_add_not_infinite_neg Hyperreal.infinitePos_add_not_infiniteNeg

theorem not_infiniteNeg_add_infinitePos {x y : ‚Ñù*} :
    ¬¨InfiniteNeg x ‚Üí InfinitePos y ‚Üí InfinitePos (x + y) := fun hx hy =>
  add_comm y x ‚ñ∏ infinitePos_add_not_infiniteNeg hy hx
#align hyperreal.not_infinite_neg_add_infinite_pos Hyperreal.not_infiniteNeg_add_infinitePos

theorem infiniteNeg_add_not_infinitePos {x y : ‚Ñù*} :
    InfiniteNeg x ‚Üí ¬¨InfinitePos y ‚Üí InfiniteNeg (x + y) := by
  rw [‚Üê infinitePos_neg, ‚Üê infinitePos_neg, ‚Üê @infiniteNeg_neg y, neg_add]
  exact infinitePos_add_not_infiniteNeg
#align hyperreal.infinite_neg_add_not_infinite_pos Hyperreal.infiniteNeg_add_not_infinitePos

theorem not_infinitePos_add_infiniteNeg {x y : ‚Ñù*} :
    ¬¨InfinitePos x ‚Üí InfiniteNeg y ‚Üí InfiniteNeg (x + y) := fun hx hy =>
  add_comm y x ‚ñ∏ infiniteNeg_add_not_infinitePos hy hx
#align hyperreal.not_infinite_pos_add_infinite_neg Hyperreal.not_infinitePos_add_infiniteNeg

theorem infinitePos_add_infinitePos {x y : ‚Ñù*} :
    InfinitePos x ‚Üí InfinitePos y ‚Üí InfinitePos (x + y) := fun hx hy =>
  infinitePos_add_not_infiniteNeg hx hy.not_infiniteNeg
#align hyperreal.infinite_pos_add_infinite_pos Hyperreal.infinitePos_add_infinitePos

theorem infiniteNeg_add_infiniteNeg {x y : ‚Ñù*} :
    InfiniteNeg x ‚Üí InfiniteNeg y ‚Üí InfiniteNeg (x + y) := fun hx hy =>
  infiniteNeg_add_not_infinitePos hx hy.not_infinitePos
#align hyperreal.infinite_neg_add_infinite_neg Hyperreal.infiniteNeg_add_infiniteNeg

theorem infinitePos_add_not_infinite {x y : ‚Ñù*} :
    InfinitePos x ‚Üí ¬¨Infinite y ‚Üí InfinitePos (x + y) := fun hx hy =>
  infinitePos_add_not_infiniteNeg hx (not_or.mp hy).2
#align hyperreal.infinite_pos_add_not_infinite Hyperreal.infinitePos_add_not_infinite

theorem infiniteNeg_add_not_infinite {x y : ‚Ñù*} :
    InfiniteNeg x ‚Üí ¬¨Infinite y ‚Üí InfiniteNeg (x + y) := fun hx hy =>
  infiniteNeg_add_not_infinitePos hx (not_or.mp hy).1
#align hyperreal.infinite_neg_add_not_infinite Hyperreal.infiniteNeg_add_not_infinite

theorem infinitePos_of_tendsto_top {f : ‚Ñï ‚Üí ‚Ñù} (hf : Tendsto f atTop atTop) :
    InfinitePos (ofSeq f) := fun r =>
  have hf' := tendsto_atTop_atTop.mp hf
  let ‚ü®i, hi‚ü© := hf' (r + 1)
  have hi' : ‚àÄ a : ‚Ñï, f a < r + 1 ‚Üí a < i := fun a => lt_imp_lt_of_le_imp_le (hi a)
  have hS : { a : ‚Ñï | r < f a }·∂ú ‚äÜ { a : ‚Ñï | a ‚â§ i } := by
    simp only [Set.compl_setOf, not_lt]
    exact fun a har => le_of_lt (hi' a (lt_of_le_of_lt har (lt_add_one _)))
  Germ.coe_lt.2 <| mem_hyperfilter_of_finite_compl <| (Set.finite_le_nat _).subset hS
#align hyperreal.infinite_pos_of_tendsto_top Hyperreal.infinitePos_of_tendsto_top

theorem infiniteNeg_of_tendsto_bot {f : ‚Ñï ‚Üí ‚Ñù} (hf : Tendsto f atTop atBot) :
    InfiniteNeg (ofSeq f) := fun r =>
  have hf' := tendsto_atTop_atBot.mp hf
  let ‚ü®i, hi‚ü© := hf' (r - 1)
  have hi' : ‚àÄ a : ‚Ñï, r - 1 < f a ‚Üí a < i := fun a => lt_imp_lt_of_le_imp_le (hi a)
  have hS : { a : ‚Ñï | f a < r }·∂ú ‚äÜ { a : ‚Ñï | a ‚â§ i } := by
    simp only [Set.compl_setOf, not_lt]
    exact fun a har => le_of_lt (hi' a (lt_of_lt_of_le (sub_one_lt _) har))
  Germ.coe_lt.2 <| mem_hyperfilter_of_finite_compl <| (Set.finite_le_nat _).subset hS
#align hyperreal.infinite_neg_of_tendsto_bot Hyperreal.infiniteNeg_of_tendsto_bot

theorem not_infinite_neg {x : ‚Ñù*} : ¬¨Infinite x ‚Üí ¬¨Infinite (-x) := mt infinite_neg.mp
#align hyperreal.not_infinite_neg Hyperreal.not_infinite_neg

theorem not_infinite_add {x y : ‚Ñù*} (hx : ¬¨Infinite x) (hy : ¬¨Infinite y) : ¬¨Infinite (x + y) :=
  have ‚ü®r, hr‚ü© := exists_st_of_not_infinite hx
  have ‚ü®s, hs‚ü© := exists_st_of_not_infinite hy
  not_infinite_of_exists_st <| ‚ü®r + s, hr.add hs‚ü©
#align hyperreal.not_infinite_add Hyperreal.not_infinite_add

theorem not_infinite_iff_exist_lt_gt {x : ‚Ñù*} : ¬¨Infinite x ‚Üî ‚àÉ r s : ‚Ñù, (r : ‚Ñù*) < x ‚àß x < s :=
  ‚ü®fun hni ‚Ü¶ let ‚ü®r, hr‚ü© := exists_st_of_not_infinite hni; ‚ü®r - 1, r + 1, hr 1 one_pos‚ü©,
    fun ‚ü®r, s, hr, hs‚ü© hi ‚Ü¶ hi.elim (fun hp ‚Ü¶ (hp s).not_lt hs) (fun hn ‚Ü¶ (hn r).not_lt hr)‚ü©
#align hyperreal.not_infinite_iff_exist_lt_gt Hyperreal.not_infinite_iff_exist_lt_gt

theorem not_infinite_real (r : ‚Ñù) : ¬¨Infinite r := by
  rw [not_infinite_iff_exist_lt_gt]
  exact ‚ü®r - 1, r + 1, coe_lt_coe.2 <| sub_one_lt r, coe_lt_coe.2 <| lt_add_one r‚ü©
#align hyperreal.not_infinite_real Hyperreal.not_infinite_real

theorem Infinite.ne_real {x : ‚Ñù*} : Infinite x ‚Üí ‚àÄ r : ‚Ñù, x ‚â† r := fun hi r hr =>
  not_infinite_real r <| @Eq.subst _ Infinite _ _ hr hi
#align hyperreal.not_real_of_infinite Hyperreal.Infinite.ne_real

/-!
### Facts about `st` that require some infinite machinery
-/

theorem IsSt.mul {x y : ‚Ñù*} {r s : ‚Ñù} (hxr : IsSt x r) (hys : IsSt y s) : IsSt (x * y) (r * s) :=
  hxr.map‚ÇÇ hys continuous_mul.continuousAt
#align hyperreal.is_st_mul Hyperreal.IsSt.mul

--AN INFINITE LEMMA THAT REQUIRES SOME MORE ST MACHINERY
theorem not_infinite_mul {x y : ‚Ñù*} (hx : ¬¨Infinite x) (hy : ¬¨Infinite y) : ¬¨Infinite (x * y) :=
  have ‚ü®_r, hr‚ü© := exists_st_of_not_infinite hx
  have ‚ü®_s, hs‚ü© := exists_st_of_not_infinite hy
  (hr.mul hs).not_infinite
#align hyperreal.not_infinite_mul Hyperreal.not_infinite_mul

---
theorem st_add {x y : ‚Ñù*} (hx : ¬¨Infinite x) (hy : ¬¨Infinite y) : st (x + y) = st x + st y :=
  (isSt_st' (not_infinite_add hx hy)).unique ((isSt_st' hx).add (isSt_st' hy))
#align hyperreal.st_add Hyperreal.st_add

theorem st_neg (x : ‚Ñù*) : st (-x) = -st x :=
  if h : Infinite x then by
    rw [h.st_eq, (infinite_neg.2 h).st_eq, neg_zero]
  else (isSt_st' (not_infinite_neg h)).unique (isSt_st' h).neg
#align hyperreal.st_neg Hyperreal.st_neg

theorem st_mul {x y : ‚Ñù*} (hx : ¬¨Infinite x) (hy : ¬¨Infinite y) : st (x * y) = st x * st y :=
  have hx' := isSt_st' hx
  have hy' := isSt_st' hy
  have hxy := isSt_st' (not_infinite_mul hx hy)
  hxy.unique (hx'.mul hy')
#align hyperreal.st_mul Hyperreal.st_mul

/-!
### Basic lemmas about infinitesimal
-/

theorem infinitesimal_def {x : ‚Ñù*} : Infinitesimal x ‚Üî ‚àÄ r : ‚Ñù, 0 < r ‚Üí -(r : ‚Ñù*) < x ‚àß x < r := by
  simp [Infinitesimal, IsSt]
#align hyperreal.infinitesimal_def Hyperreal.infinitesimal_def

theorem lt_of_pos_of_infinitesimal {x : ‚Ñù*} : Infinitesimal x ‚Üí ‚àÄ r : ‚Ñù, 0 < r ‚Üí x < r :=
  fun hi r hr => ((infinitesimal_def.mp hi) r hr).2
#align hyperreal.lt_of_pos_of_infinitesimal Hyperreal.lt_of_pos_of_infinitesimal

theorem lt_neg_of_pos_of_infinitesimal {x : ‚Ñù*} : Infinitesimal x ‚Üí ‚àÄ r : ‚Ñù, 0 < r ‚Üí -‚Üër < x :=
  fun hi r hr => ((infinitesimal_def.mp hi) r hr).1
#align hyperreal.lt_neg_of_pos_of_infinitesimal Hyperreal.lt_neg_of_pos_of_infinitesimal

theorem gt_of_neg_of_infinitesimal {x : ‚Ñù*} (hi : Infinitesimal x) (r : ‚Ñù) (hr : r < 0) : ‚Üër < x :=
  neg_neg r ‚ñ∏ (infinitesimal_def.1 hi (-r) (neg_pos.2 hr)).1
#align hyperreal.gt_of_neg_of_infinitesimal Hyperreal.gt_of_neg_of_infinitesimal

theorem abs_lt_real_iff_infinitesimal {x : ‚Ñù*} : Infinitesimal x ‚Üî ‚àÄ r : ‚Ñù, r ‚â† 0 ‚Üí |x| < |‚Üër| :=
  ‚ü®fun hi r hr ‚Ü¶ abs_lt.mpr (coe_abs r ‚ñ∏ infinitesimal_def.mp hi |r| (abs_pos.2 hr)), fun hR ‚Ü¶
    infinitesimal_def.mpr fun r hr => abs_lt.mp <| (abs_of_pos <| coe_pos.2 hr) ‚ñ∏ hR r <| hr.ne'‚ü©
#align hyperreal.abs_lt_real_iff_infinitesimal Hyperreal.abs_lt_real_iff_infinitesimal

theorem infinitesimal_zero : Infinitesimal 0 := isSt_refl_real 0
#align hyperreal.infinitesimal_zero Hyperreal.infinitesimal_zero

theorem Infinitesimal.eq_zero {r : ‚Ñù} : Infinitesimal r ‚Üí r = 0 := eq_of_isSt_real
#align hyperreal.zero_of_infinitesimal_real Hyperreal.Infinitesimal.eq_zero

-- Porting note: swapped LHS with RHS; added `@[simp]`
@[simp] theorem infinitesimal_real_iff {r : ‚Ñù} : Infinitesimal r ‚Üî r = 0 :=
  isSt_real_iff_eq
#align hyperreal.zero_iff_infinitesimal_real Hyperreal.infinitesimal_real_iff

nonrec theorem Infinitesimal.add {x y : ‚Ñù*} (hx : Infinitesimal x) (hy : Infinitesimal y) :
    Infinitesimal (x + y) := by simpa only [add_zero] using hx.add hy
#align hyperreal.infinitesimal_add Hyperreal.Infinitesimal.add

nonrec theorem Infinitesimal.neg {x : ‚Ñù*} (hx : Infinitesimal x) : Infinitesimal (-x) := by
  simpa only [neg_zero] using hx.neg
#align hyperreal.infinitesimal_neg Hyperreal.Infinitesimal.neg

-- Porting note: swapped LHS and RHS, added `@[simp]`
@[simp] theorem infinitesimal_neg {x : ‚Ñù*} : Infinitesimal (-x) ‚Üî Infinitesimal x :=
  ‚ü®fun h => neg_neg x ‚ñ∏ h.neg, Infinitesimal.neg‚ü©
#align hyperreal.infinitesimal_neg_iff Hyperreal.infinitesimal_neg‚Çì

nonrec theorem Infinitesimal.mul {x y : ‚Ñù*} (hx : Infinitesimal x) (hy : Infinitesimal y) :
    Infinitesimal (x * y) := by simpa only [mul_zero] using hx.mul hy
#align hyperreal.infinitesimal_mul Hyperreal.Infinitesimal.mul

theorem infinitesimal_of_tendsto_zero {f : ‚Ñï ‚Üí ‚Ñù} (h : Tendsto f atTop (ùìù 0)) :
    Infinitesimal (ofSeq f) :=
  isSt_of_tendsto h
#align hyperreal.infinitesimal_of_tendsto_zero Hyperreal.infinitesimal_of_tendsto_zero

theorem infinitesimal_epsilon : Infinitesimal Œµ :=
  infinitesimal_of_tendsto_zero tendsto_inverse_atTop_nhds_zero_nat
#align hyperreal.infinitesimal_epsilon Hyperreal.infinitesimal_epsilon

theorem not_real_of_infinitesimal_ne_zero (x : ‚Ñù*) : Infinitesimal x ‚Üí x ‚â† 0 ‚Üí ‚àÄ r : ‚Ñù, x ‚â† r :=
  fun hi hx r hr =>
  hx <| hr.trans <| coe_eq_zero.2 <| IsSt.unique (hr.symm ‚ñ∏ isSt_refl_real r : IsSt x r) hi
#align hyperreal.not_real_of_infinitesimal_ne_zero Hyperreal.not_real_of_infinitesimal_ne_zero

theorem IsSt.infinitesimal_sub {x : ‚Ñù*} {r : ‚Ñù} (hxr : IsSt x r) : Infinitesimal (x - ‚Üër) := by
  simpa only [sub_self] using hxr.sub (isSt_refl_real r)
#align hyperreal.infinitesimal_sub_is_st Hyperreal.IsSt.infinitesimal_sub

theorem infinitesimal_sub_st {x : ‚Ñù*} (hx : ¬¨Infinite x) : Infinitesimal (x - ‚Üë(st x)) :=
  (isSt_st' hx).infinitesimal_sub
#align hyperreal.infinitesimal_sub_st Hyperreal.infinitesimal_sub_st

theorem infinitePos_iff_infinitesimal_inv_pos {x : ‚Ñù*} :
    InfinitePos x ‚Üî Infinitesimal x‚Åª¬π ‚àß 0 < x‚Åª¬π :=
  ‚ü®fun hip =>
    ‚ü®infinitesimal_def.mpr fun r hr =>
        ‚ü®lt_trans (coe_lt_coe.2 (neg_neg_of_pos hr)) (inv_pos.2 (hip 0)),
          (inv_lt (coe_lt_coe.2 hr) (hip 0)).mp (by convert hip r‚Åª¬π)‚ü©,
      inv_pos.2 <| hip 0‚ü©,
    fun ‚ü®hi, hp‚ü© r =>
    @_root_.by_cases (r = 0) (‚Üër < x) (fun h => Eq.substr h (inv_pos.mp hp)) fun h =>
      lt_of_le_of_lt (coe_le_coe.2 (le_abs_self r))
        ((inv_lt_inv (inv_pos.mp hp) (coe_lt_coe.2 (abs_pos.2 h))).mp
          ((infinitesimal_def.mp hi) |r|‚Åª¬π (inv_pos.2 (abs_pos.2 h))).2)‚ü©
#align hyperreal.infinite_pos_iff_infinitesimal_inv_pos Hyperreal.infinitePos_iff_infinitesimal_inv_pos

theorem infiniteNeg_iff_infinitesimal_inv_neg {x : ‚Ñù*} :
    InfiniteNeg x ‚Üî Infinitesimal x‚Åª¬π ‚àß x‚Åª¬π < 0 := by
  rw [‚Üê infinitePos_neg, infinitePos_iff_infinitesimal_inv_pos, inv_neg, neg_pos, infinitesimal_neg]
#align hyperreal.infinite_neg_iff_infinitesimal_inv_neg Hyperreal.infiniteNeg_iff_infinitesimal_inv_neg

theorem infinitesimal_inv_of_infinite {x : ‚Ñù*} : Infinite x ‚Üí Infinitesimal x‚Åª¬π := fun hi =>
  Or.casesOn hi (fun hip => (infinitePos_iff_infinitesimal_inv_pos.mp hip).1) fun hin =>
    (infiniteNeg_iff_infinitesimal_inv_neg.mp hin).1
#align hyperreal.infinitesimal_inv_of_infinite Hyperreal.infinitesimal_inv_of_infinite

theorem infinite_of_infinitesimal_inv {x : ‚Ñù*} (h0 : x ‚â† 0) (hi : Infinitesimal x‚Åª¬π) :
    Infinite x := by
  cases' lt_or_gt_of_ne h0 with hn hp
  ¬∑ exact Or.inr (infiniteNeg_iff_infinitesimal_inv_neg.mpr ‚ü®hi, inv_lt_zero.mpr hn‚ü©)
  ¬∑ exact Or.inl (infinitePos_iff_infinitesimal_inv_pos.mpr ‚ü®hi, inv_pos.mpr hp‚ü©)
#align hyperreal.infinite_of_infinitesimal_inv Hyperreal.infinite_of_infinitesimal_inv

theorem infinite_iff_infinitesimal_inv {x : ‚Ñù*} (h0 : x ‚â† 0) : Infinite x ‚Üî Infinitesimal x‚Åª¬π :=
  ‚ü®infinitesimal_inv_of_infinite, infinite_of_infinitesimal_inv h0‚ü©
#align hyperreal.infinite_iff_infinitesimal_inv Hyperreal.infinite_iff_infinitesimal_inv

theorem infinitesimal_pos_iff_infinitePos_inv {x : ‚Ñù*} :
    InfinitePos x‚Åª¬π ‚Üî Infinitesimal x ‚àß 0 < x :=
  infinitePos_iff_infinitesimal_inv_pos.trans <| by rw [inv_inv]
#align hyperreal.infinitesimal_pos_iff_infinite_pos_inv Hyperreal.infinitesimal_pos_iff_infinitePos_inv

theorem infinitesimal_neg_iff_infiniteNeg_inv {x : ‚Ñù*} :
    InfiniteNeg x‚Åª¬π ‚Üî Infinitesimal x ‚àß x < 0 :=
  infiniteNeg_iff_infinitesimal_inv_neg.trans <| by rw [inv_inv]
#align hyperreal.infinitesimal_neg_iff_infinite_neg_inv Hyperreal.infinitesimal_neg_iff_infiniteNeg_inv

theorem infinitesimal_iff_infinite_inv {x : ‚Ñù*} (h : x ‚â† 0) : Infinitesimal x ‚Üî Infinite x‚Åª¬π :=
  Iff.trans (by rw [inv_inv]) (infinite_iff_infinitesimal_inv (inv_ne_zero h)).symm
#align hyperreal.infinitesimal_iff_infinite_inv Hyperreal.infinitesimal_iff_infinite_inv

/-!
### `Hyperreal.st` stuff that requires infinitesimal machinery
-/

theorem IsSt.inv {x : ‚Ñù*} {r : ‚Ñù} (hi : ¬¨Infinitesimal x) (hr : IsSt x r) : IsSt x‚Åª¬π r‚Åª¬π :=
  hr.map <| continuousAt_inv‚ÇÄ <| by rintro rfl; exact hi hr
#align hyperreal.is_st_inv Hyperreal.IsSt.inv

theorem st_inv (x : ‚Ñù*) : st x‚Åª¬π = (st x)‚Åª¬π := by
  by_cases h0 : x = 0
  ¬∑ rw [h0, inv_zero, ‚Üê coe_zero, st_id_real, inv_zero]
  by_cases h1 : Infinitesimal x
  ¬∑ rw [((infinitesimal_iff_infinite_inv h0).mp h1).st_eq, h1.st_eq, inv_zero]
  by_cases h2 : Infinite x
  ¬∑ rw [(infinitesimal_inv_of_infinite h2).st_eq, h2.st_eq, inv_zero]
  exact ((isSt_st' h2).inv h1).st_eq
#align hyperreal.st_inv Hyperreal.st_inv

/-!
### Infinite stuff that requires infinitesimal machinery
-/

theorem infinitePos_omega : InfinitePos œâ :=
  infinitePos_iff_infinitesimal_inv_pos.mpr ‚ü®infinitesimal_epsilon, epsilon_pos‚ü©
#align hyperreal.infinite_pos_omega Hyperreal.infinitePos_omega

theorem infinite_omega : Infinite œâ :=
  (infinite_iff_infinitesimal_inv omega_ne_zero).mpr infinitesimal_epsilon
#align hyperreal.infinite_omega Hyperreal.infinite_omega

theorem infinitePos_mul_of_infinitePos_not_infinitesimal_pos {x y : ‚Ñù*} :
    InfinitePos x ‚Üí ¬¨Infinitesimal y ‚Üí 0 < y ‚Üí InfinitePos (x * y) := fun hx hy‚ÇÅ hy‚ÇÇ r => by
  have hy‚ÇÅ' := not_forall.mp (mt infinitesimal_def.2 hy‚ÇÅ)
  let ‚ü®r‚ÇÅ, hy‚ÇÅ''‚ü© := hy‚ÇÅ'
  have hyr : 0 < r‚ÇÅ ‚àß ‚Üër‚ÇÅ ‚â§ y := by
    rwa [Classical.not_imp, ‚Üê abs_lt, not_lt, abs_of_pos hy‚ÇÇ] at hy‚ÇÅ''
  rw [‚Üê div_mul_cancel‚ÇÄ r (ne_of_gt hyr.1), coe_mul]
  exact mul_lt_mul (hx (r / r‚ÇÅ)) hyr.2 (coe_lt_coe.2 hyr.1) (le_of_lt (hx 0))
#align hyperreal.infinite_pos_mul_of_infinite_pos_not_infinitesimal_pos Hyperreal.infinitePos_mul_of_infinitePos_not_infinitesimal_pos

theorem infinitePos_mul_of_not_infinitesimal_pos_infinitePos {x y : ‚Ñù*} :
    ¬¨Infinitesimal x ‚Üí 0 < x ‚Üí InfinitePos y ‚Üí InfinitePos (x * y) := fun hx hp hy =>
  mul_comm y x ‚ñ∏ infinitePos_mul_of_infinitePos_not_infinitesimal_pos hy hx hp
#align hyperreal.infinite_pos_mul_of_not_infinitesimal_pos_infinite_pos Hyperreal.infinitePos_mul_of_not_infinitesimal_pos_infinitePos

theorem infinitePos_mul_of_infiniteNeg_not_infinitesimal_neg {x y : ‚Ñù*} :
    InfiniteNeg x ‚Üí ¬¨Infinitesimal y ‚Üí y < 0 ‚Üí InfinitePos (x * y) := by
  rw [‚Üê infinitePos_neg, ‚Üê neg_pos, ‚Üê neg_mul_neg, ‚Üê infinitesimal_neg]
  exact infinitePos_mul_of_infinitePos_not_infinitesimal_pos
#align hyperreal.infinite_pos_mul_of_infinite_neg_not_infinitesimal_neg Hyperreal.infinitePos_mul_of_infiniteNeg_not_infinitesimal_neg

theorem infinitePos_mul_of_not_infinitesimal_neg_infiniteNeg {x y : ‚Ñù*} :
    ¬¨Infinitesimal x ‚Üí x < 0 ‚Üí InfiniteNeg y ‚Üí InfinitePos (x * y) := fun hx hp hy =>
  mul_comm y x ‚ñ∏ infinitePos_mul_of_infiniteNeg_not_infinitesimal_neg hy hx hp
#align hyperreal.infinite_pos_mul_of_not_infinitesimal_neg_infinite_neg Hyperreal.infinitePos_mul_of_not_infinitesimal_neg_infiniteNeg

theorem infiniteNeg_mul_of_infinitePos_not_infinitesimal_neg {x y : ‚Ñù*} :
    InfinitePos x ‚Üí ¬¨Infinitesimal y ‚Üí y < 0 ‚Üí InfiniteNeg (x * y) := by
  rw [‚Üê infinitePos_neg, ‚Üê neg_pos, neg_mul_eq_mul_neg, ‚Üê infinitesimal_neg]
  exact infinitePos_mul_of_infinitePos_not_infinitesimal_pos
#align hyperreal.infinite_neg_mul_of_infinite_pos_not_infinitesimal_neg Hyperreal.infiniteNeg_mul_of_infinitePos_not_infinitesimal_neg

theorem infiniteNeg_mul_of_not_infinitesimal_neg_infinitePos {x y : ‚Ñù*} :
    ¬¨Infinitesimal x ‚Üí x < 0 ‚Üí InfinitePos y ‚Üí InfiniteNeg (x * y) := fun hx hp hy =>
  mul_comm y x ‚ñ∏ infiniteNeg_mul_of_infinitePos_not_infinitesimal_neg hy hx hp
#align hyperreal.infinite_neg_mul_of_not_infinitesimal_neg_infinite_pos Hyperreal.infiniteNeg_mul_of_not_infinitesimal_neg_infinitePos

theorem infiniteNeg_mul_of_infiniteNeg_not_infinitesimal_pos {x y : ‚Ñù*} :
    InfiniteNeg x ‚Üí ¬¨Infinitesimal y ‚Üí 0 < y ‚Üí InfiniteNeg (x * y) := by
  rw [‚Üê infinitePos_neg, ‚Üê infinitePos_neg, neg_mul_eq_neg_mul]
  exact infinitePos_mul_of_infinitePos_not_infinitesimal_pos
#align hyperreal.infinite_neg_mul_of_infinite_neg_not_infinitesimal_pos Hyperreal.infiniteNeg_mul_of_infiniteNeg_not_infinitesimal_pos

theorem infiniteNeg_mul_of_not_infinitesimal_pos_infiniteNeg {x y : ‚Ñù*} :
    ¬¨Infinitesimal x ‚Üí 0 < x ‚Üí InfiniteNeg y ‚Üí InfiniteNeg (x * y) := fun hx hp hy => by
  rw [mul_comm]; exact infiniteNeg_mul_of_infiniteNeg_not_infinitesimal_pos hy hx hp
#align hyperreal.infinite_neg_mul_of_not_infinitesimal_pos_infinite_neg Hyperreal.infiniteNeg_mul_of_not_infinitesimal_pos_infiniteNeg

theorem infinitePos_mul_infinitePos {x y : ‚Ñù*} :
    InfinitePos x ‚Üí InfinitePos y ‚Üí InfinitePos (x * y) := fun hx hy =>
  infinitePos_mul_of_infinitePos_not_infinitesimal_pos hx hy.not_infinitesimal (hy 0)
#align hyperreal.infinite_pos_mul_infinite_pos Hyperreal.infinitePos_mul_infinitePos

theorem infiniteNeg_mul_infiniteNeg {x y : ‚Ñù*} :
    InfiniteNeg x ‚Üí InfiniteNeg y ‚Üí InfinitePos (x * y) := fun hx hy =>
  infinitePos_mul_of_infiniteNeg_not_infinitesimal_neg hx hy.not_infinitesimal (hy 0)
#align hyperreal.infinite_neg_mul_infinite_neg Hyperreal.infiniteNeg_mul_infiniteNeg

theorem infinitePos_mul_infiniteNeg {x y : ‚Ñù*} :
    InfinitePos x ‚Üí InfiniteNeg y ‚Üí InfiniteNeg (x * y) := fun hx hy =>
  infiniteNeg_mul_of_infinitePos_not_infinitesimal_neg hx hy.not_infinitesimal (hy 0)
#align hyperreal.infinite_pos_mul_infinite_neg Hyperreal.infinitePos_mul_infiniteNeg

theorem infiniteNeg_mul_infinitePos {x y : ‚Ñù*} :
    InfiniteNeg x ‚Üí InfinitePos y ‚Üí InfiniteNeg (x * y) := fun hx hy =>
  infiniteNeg_mul_of_infiniteNeg_not_infinitesimal_pos hx hy.not_infinitesimal (hy 0)
#align hyperreal.infinite_neg_mul_infinite_pos Hyperreal.infiniteNeg_mul_infinitePos

theorem infinite_mul_of_infinite_not_infinitesimal {x y : ‚Ñù*} :
    Infinite x ‚Üí ¬¨Infinitesimal y ‚Üí Infinite (x * y) := fun hx hy =>
  have h0 : y < 0 ‚à® 0 < y := lt_or_gt_of_ne fun H0 => hy (Eq.substr H0 (isSt_refl_real 0))
  hx.elim
    (h0.elim
      (fun H0 Hx => Or.inr (infiniteNeg_mul_of_infinitePos_not_infinitesimal_neg Hx hy H0))
      fun H0 Hx => Or.inl (infinitePos_mul_of_infinitePos_not_infinitesimal_pos Hx hy H0))
    (h0.elim
      (fun H0 Hx => Or.inl (infinitePos_mul_of_infiniteNeg_not_infinitesimal_neg Hx hy H0))
      fun H0 Hx => Or.inr (infiniteNeg_mul_of_infiniteNeg_not_infinitesimal_pos Hx hy H0))
#align hyperreal.infinite_mul_of_infinite_not_infinitesimal Hyperreal.infinite_mul_of_infinite_not_infinitesimal

theorem infinite_mul_of_not_infinitesimal_infinite {x y : ‚Ñù*} :
    ¬¨Infinitesimal x ‚Üí Infinite y ‚Üí Infinite (x * y) := fun hx hy => by
  rw [mul_comm]; exact infinite_mul_of_infinite_not_infinitesimal hy hx
#align hyperreal.infinite_mul_of_not_infinitesimal_infinite Hyperreal.infinite_mul_of_not_infinitesimal_infinite

theorem Infinite.mul {x y : ‚Ñù*} : Infinite x ‚Üí Infinite y ‚Üí Infinite (x * y) := fun hx hy =>
  infinite_mul_of_infinite_not_infinitesimal hx hy.not_infinitesimal
#align hyperreal.infinite.mul Hyperreal.Infinite.mul

end Hyperreal

/-
Porting note (#11215): TODO: restore `positivity` plugin

namespace Tactic

open Positivity

private theorem hyperreal_coe_ne_zero {r : ‚Ñù} : r ‚â† 0 ‚Üí (r : ‚Ñù*) ‚â† 0 :=
  Hyperreal.coe_ne_zero.2
#align tactic.hyperreal_coe_ne_zero tactic.hyperreal_coe_ne_zero

private theorem hyperreal_coe_nonneg {r : ‚Ñù} : 0 ‚â§ r ‚Üí 0 ‚â§ (r : ‚Ñù*) :=
  Hyperreal.coe_nonneg.2
#align tactic.hyperreal_coe_nonneg tactic.hyperreal_coe_nonneg

private theorem hyperreal_coe_pos {r : ‚Ñù} : 0 < r ‚Üí 0 < (r : ‚Ñù*) :=
  Hyperreal.coe_pos.2
#align tactic.hyperreal_coe_pos tactic.hyperreal_coe_pos

/-- Extension for the `positivity` tactic: cast from `‚Ñù` to `‚Ñù*`. -/
@[positivity]
unsafe def positivity_coe_real_hyperreal : expr ‚Üí tactic strictness
  | q(@coe _ _ $(inst) $(a)) => do
    unify inst q(@coeToLift _ _ Hyperreal.hasCoeT)
    let strictness_a ‚Üê core a
    match strictness_a with
      | positive p => positive <$> mk_app `` hyperreal_coe_pos [p]
      | nonnegative p => nonnegative <$> mk_app `` hyperreal_coe_nonneg [p]
      | nonzero p => nonzero <$> mk_app `` hyperreal_coe_ne_zero [p]
  | e =>
    pp e >>= fail ‚àò format.bracket "The expression " " is not of the form `(r : ‚Ñù*)` for `r : ‚Ñù`"
#align tactic.positivity_coe_real_hyperreal tactic.positivity_coe_real_hyperreal

end Tactic
-/
