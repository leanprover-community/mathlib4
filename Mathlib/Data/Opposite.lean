/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Reid Barton, Simon Hudon, Kenny Lau
-/
import Mathlib.Logic.Equiv.Defs

#align_import data.opposite from "leanprover-community/mathlib"@"99e8971dc62f1f7ecf693d75e75fbbabd55849de"

/-!
# Opposites

In this file we define a structure `Opposite Î±` containing a single field of type `Î±` and
two bijections `op : Î± â†’ Î±áµ’áµ–` and `unop : Î±áµ’áµ– â†’ Î±`. If `Î±` is a category, then `Î±áµ’áµ–` is the
opposite category, with all arrows reversed.

-/


universe v u

-- morphism levels before object levels. See note [CategoryTheory universes].
variable (Î± : Sort u)

-- porting note: in mathlib, `opposite Î±` was a type synonym for `Î±`, but if we did
-- the same in Lean4, one could write problematic definitions like:
-- example (X : C) : Cáµ’áµ– := X
-- example {X Y : C} (f : X âŸ¶ Y): op Y âŸ¶ op X := f
/-- The type of objects of the opposite of `Î±`; used to define the opposite category.

  Now that Lean 4 supports definitional eta equality for records,
  both `unop (op X) = X` and `op (unop X) = X` are definitional equalities.

-/
structure Opposite :=
  /-- The canonical map `Î±áµ’áµ– â†’ Î±`. -/
  unop : Î±
#align opposite Opposite
#align opposite.unop Opposite.unop

@[inherit_doc]
notation:max -- Use a high right binding power (like that of postfix â»Â¹) so that, for example,
-- `Presheaf Cáµ’áµ–` parses as `Presheaf (Cáµ’áµ–)` and not `(Presheaf C)áµ’áµ–`.
Î± "áµ’áµ–" => Opposite Î±

namespace Opposite

variable {Î±}

/-- The canonical map `Î± â†’ Î±áµ’áµ–`. -/
-- Porting note: pp_nodot has not been implemented.
--@[pp_nodot]
def op (x : Î±) : Î±áµ’áµ– := âŸ¨xâŸ©
#align opposite.op Opposite.op

theorem op_injective : Function.Injective (op : Î± â†’ Î±áµ’áµ–) := fun _ _ => congr_arg Opposite.unop
#align opposite.op_injective Opposite.op_injective

theorem unop_injective : Function.Injective (unop : Î±áµ’áµ– â†’ Î±) := fun âŸ¨_âŸ©âŸ¨_âŸ© => by simp
                                                                                 -- ðŸŽ‰ no goals
#align opposite.unop_injective Opposite.unop_injective

@[simp]
theorem op_unop (x : Î±áµ’áµ–) : op (unop x) = x :=
  rfl
#align opposite.op_unop Opposite.op_unop

@[simp]
theorem unop_op (x : Î±) : unop (op x) = x :=
  rfl
#align opposite.unop_op Opposite.unop_op

-- We could prove these by `Iff.rfl`, but that would make these eligible for `dsimp`. That would be
-- a bad idea because `Opposite` is irreducible.
@[simp]
theorem op_inj_iff (x y : Î±) : op x = op y â†” x = y :=
  op_injective.eq_iff
#align opposite.op_inj_iff Opposite.op_inj_iff

@[simp]
theorem unop_inj_iff (x y : Î±áµ’áµ–) : unop x = unop y â†” x = y :=
  unop_injective.eq_iff
#align opposite.unop_inj_iff Opposite.unop_inj_iff

/-- The type-level equivalence between a type and its opposite. -/
def equivToOpposite : Î± â‰ƒ Î±áµ’áµ– where
  toFun := op
  invFun := unop
  left_inv := unop_op
  right_inv := op_unop
#align opposite.equiv_to_opposite Opposite.equivToOpposite

@[simp]
theorem equivToOpposite_coe : (equivToOpposite : Î± â†’ Î±áµ’áµ–) = op :=
  rfl
#align opposite.equiv_to_opposite_coe Opposite.equivToOpposite_coe

@[simp]
theorem equivToOpposite_symm_coe : (equivToOpposite.symm : Î±áµ’áµ– â†’ Î±) = unop :=
  rfl
#align opposite.equiv_to_opposite_symm_coe Opposite.equivToOpposite_symm_coe

theorem op_eq_iff_eq_unop {x : Î±} {y} : op x = y â†” x = unop y :=
  equivToOpposite.apply_eq_iff_eq_symm_apply
#align opposite.op_eq_iff_eq_unop Opposite.op_eq_iff_eq_unop

theorem unop_eq_iff_eq_op {x} {y : Î±} : unop x = y â†” x = op y :=
  equivToOpposite.symm.apply_eq_iff_eq_symm_apply
#align opposite.unop_eq_iff_eq_op Opposite.unop_eq_iff_eq_op

instance [Inhabited Î±] : Inhabited Î±áµ’áµ– :=
  âŸ¨op defaultâŸ©

/-- A recursor for `Opposite`.
The `@[eliminator]` attribute makes it the default induction principle for `Opposite`
so you don't need to use `induction x using Opposite.rec'`. -/
@[simp, eliminator]
protected def rec' {F : Î±áµ’áµ– â†’ Sort v} (h : âˆ€ X, F (op X)) : âˆ€ X, F X := fun X => h (unop X)
#align opposite.rec Opposite.rec'

end Opposite
