/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Data.Complex.Module

/-!
# The partial order on the complex numbers

This order is defined by `z ‚â§ w ‚Üî z.re ‚â§ w.re ‚àß z.im = w.im`.

This is a natural order on `‚ÑÇ` because, as is well-known, there does not exist an order on `‚ÑÇ`
making it into a `LinearOrderedField`. However, the order described above is the canonical order
stemming from the structure of `‚ÑÇ` as a ‚ãÜ-ring (i.e., it becomes a `StarOrderedRing`). Moreover,
with this order `‚ÑÇ` is a `StrictOrderedCommRing` and the coercion `(‚Üë) : ‚Ñù ‚Üí ‚ÑÇ` is an order
embedding.

This file only provides `Complex.partialOrder` and lemmas about it. Further structural classes are
provided by `Mathlib/Data/IsROrC/Basic.lean` as

* `IsROrC.toStrictOrderedCommRing`
* `IsROrC.toStarOrderedRing`
* `IsROrC.toOrderedSMul`

These are all only available with `open scoped ComplexOrder`.
-/

namespace Complex

/-- We put a partial order on ‚ÑÇ so that `z ‚â§ w` exactly if `w - z` is real and nonnegative.
Complex numbers with different imaginary parts are incomparable.
-/
protected def partialOrder : PartialOrder ‚ÑÇ where
  le z w := z.re ‚â§ w.re ‚àß z.im = w.im
  lt z w := z.re < w.re ‚àß z.im = w.im
  lt_iff_le_not_le z w := by
    dsimp
    -- ‚ä¢ z.re < w.re ‚àß z.im = w.im ‚Üî (z.re ‚â§ w.re ‚àß z.im = w.im) ‚àß ¬¨(w.re ‚â§ z.re ‚àß w. ‚Ä¶
    rw [lt_iff_le_not_le]
    -- ‚ä¢ (z.re ‚â§ w.re ‚àß ¬¨w.re ‚â§ z.re) ‚àß z.im = w.im ‚Üî (z.re ‚â§ w.re ‚àß z.im = w.im) ‚àß ¬¨ ‚Ä¶
    tauto
    -- üéâ no goals
  le_refl x := ‚ü®le_rfl, rfl‚ü©
  le_trans x y z h‚ÇÅ h‚ÇÇ := ‚ü®h‚ÇÅ.1.trans h‚ÇÇ.1, h‚ÇÅ.2.trans h‚ÇÇ.2‚ü©
  le_antisymm z w h‚ÇÅ h‚ÇÇ := ext (h‚ÇÅ.1.antisymm h‚ÇÇ.1) h‚ÇÅ.2
#align complex.partial_order Complex.partialOrder

namespace _root_.ComplexOrder

-- Porting note: made section into namespace to allow scoping
scoped[ComplexOrder] attribute [instance] Complex.partialOrder

end _root_.ComplexOrder

open ComplexOrder

theorem le_def {z w : ‚ÑÇ} : z ‚â§ w ‚Üî z.re ‚â§ w.re ‚àß z.im = w.im :=
  Iff.rfl
#align complex.le_def Complex.le_def

theorem lt_def {z w : ‚ÑÇ} : z < w ‚Üî z.re < w.re ‚àß z.im = w.im :=
  Iff.rfl
#align complex.lt_def Complex.lt_def


@[simp, norm_cast]
theorem real_le_real {x y : ‚Ñù} : (x : ‚ÑÇ) ‚â§ (y : ‚ÑÇ) ‚Üî x ‚â§ y := by simp [le_def, ofReal']
                                                                 -- üéâ no goals
#align complex.real_le_real Complex.real_le_real

@[simp, norm_cast]
theorem real_lt_real {x y : ‚Ñù} : (x : ‚ÑÇ) < (y : ‚ÑÇ) ‚Üî x < y := by simp [lt_def, ofReal']
                                                                 -- üéâ no goals
#align complex.real_lt_real Complex.real_lt_real


@[simp, norm_cast]
theorem zero_le_real {x : ‚Ñù} : (0 : ‚ÑÇ) ‚â§ (x : ‚ÑÇ) ‚Üî 0 ‚â§ x :=
  real_le_real
#align complex.zero_le_real Complex.zero_le_real

@[simp, norm_cast]
theorem zero_lt_real {x : ‚Ñù} : (0 : ‚ÑÇ) < (x : ‚ÑÇ) ‚Üî 0 < x :=
  real_lt_real
#align complex.zero_lt_real Complex.zero_lt_real

theorem not_le_iff {z w : ‚ÑÇ} : ¬¨z ‚â§ w ‚Üî w.re < z.re ‚à® z.im ‚â† w.im := by
  rw [le_def, not_and_or, not_le]
  -- üéâ no goals
#align complex.not_le_iff Complex.not_le_iff

theorem not_lt_iff {z w : ‚ÑÇ} : ¬¨z < w ‚Üî w.re ‚â§ z.re ‚à® z.im ‚â† w.im := by
  rw [lt_def, not_and_or, not_lt]
  -- üéâ no goals
#align complex.not_lt_iff Complex.not_lt_iff

theorem not_le_zero_iff {z : ‚ÑÇ} : ¬¨z ‚â§ 0 ‚Üî 0 < z.re ‚à® z.im ‚â† 0 :=
  not_le_iff
#align complex.not_le_zero_iff Complex.not_le_zero_iff

theorem not_lt_zero_iff {z : ‚ÑÇ} : ¬¨z < 0 ‚Üî 0 ‚â§ z.re ‚à® z.im ‚â† 0 :=
  not_lt_iff
#align complex.not_lt_zero_iff Complex.not_lt_zero_iff

theorem eq_re_ofReal_le {r : ‚Ñù} {z : ‚ÑÇ} (hz : (r : ‚ÑÇ) ‚â§ z) : z = z.re := by
  ext
  -- ‚ä¢ z.re = (‚Üëz.re).re
  rfl
  -- ‚ä¢ z.im = (‚Üëz.re).im
  simp only [‚Üê (Complex.le_def.1 hz).2, Complex.zero_im, Complex.ofReal_im]
  -- üéâ no goals
#align complex.eq_re_of_real_le Complex.eq_re_ofReal_le

end Complex
