/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
import Mathlib.Data.Set.Function
import Mathlib.Logic.Relation
import Mathlib.Logic.Pairwise

#align_import data.set.pairwise.basic from "leanprover-community/mathlib"@"c4c2ed622f43768eff32608d4a0f8a6cec1c047d"

/-!
# Relations holding pairwise

This file develops pairwise relations and defines pairwise disjoint indexed sets.

We also prove many basic facts about `Pairwise`. It is possible that an intermediate file,
with more imports than `Logic.Pairwise` but not importing `Data.Set.Function` would be appropriate
to hold many of these basic facts.

## Main declarations

* `Set.PairwiseDisjoint`: `s.PairwiseDisjoint f` states that images under `f` of distinct elements
  of `s` are either equal or `Disjoint`.

## Notes

The spelling `s.PairwiseDisjoint id` is preferred over `s.Pairwise Disjoint` to permit dot notation
on `Set.PairwiseDisjoint`, even though the latter unfolds to something nicer.
-/


open Function Order Set

variable {Î± Î² Î³ Î¹ Î¹' : Type*} {r p q : Î± â†’ Î± â†’ Prop}

section Pairwise

variable {f g : Î¹ â†’ Î±} {s t u : Set Î±} {a b : Î±}

theorem pairwise_on_bool (hr : Symmetric r) {a b : Î±} :
    Pairwise (r on fun c => cond c a b) â†” r a b := by simpa [Pairwise, Function.onFun] using @hr a b
                                                      -- ğŸ‰ no goals
#align pairwise_on_bool pairwise_on_bool

theorem pairwise_disjoint_on_bool [SemilatticeInf Î±] [OrderBot Î±] {a b : Î±} :
    Pairwise (Disjoint on fun c => cond c a b) â†” Disjoint a b :=
  pairwise_on_bool Disjoint.symm
#align pairwise_disjoint_on_bool pairwise_disjoint_on_bool

theorem Symmetric.pairwise_on [LinearOrder Î¹] (hr : Symmetric r) (f : Î¹ â†’ Î±) :
    Pairwise (r on f) â†” âˆ€ â¦ƒm nâ¦„, m < n â†’ r (f m) (f n) :=
  âŸ¨fun h _m _n hmn => h hmn.ne, fun h _m _n hmn => hmn.lt_or_lt.elim (@h _ _) fun h' => hr (h h')âŸ©
#align symmetric.pairwise_on Symmetric.pairwise_on

theorem pairwise_disjoint_on [SemilatticeInf Î±] [OrderBot Î±] [LinearOrder Î¹] (f : Î¹ â†’ Î±) :
    Pairwise (Disjoint on f) â†” âˆ€ â¦ƒm nâ¦„, m < n â†’ Disjoint (f m) (f n) :=
  Symmetric.pairwise_on Disjoint.symm f
#align pairwise_disjoint_on pairwise_disjoint_on

theorem pairwise_disjoint_mono [SemilatticeInf Î±] [OrderBot Î±] (hs : Pairwise (Disjoint on f))
    (h : g â‰¤ f) : Pairwise (Disjoint on g) :=
  hs.mono fun i j hij => Disjoint.mono (h i) (h j) hij
#align pairwise_disjoint.mono pairwise_disjoint_mono

namespace Set

theorem Pairwise.mono (h : t âŠ† s) (hs : s.Pairwise r) : t.Pairwise r :=
  fun _x xt _y yt => hs (h xt) (h yt)
#align set.pairwise.mono Set.Pairwise.mono

theorem Pairwise.mono' (H : r â‰¤ p) (hr : s.Pairwise r) : s.Pairwise p :=
  hr.imp H
#align set.pairwise.mono' Set.Pairwise.mono'

theorem pairwise_top (s : Set Î±) : s.Pairwise âŠ¤ :=
  pairwise_of_forall s _ fun _ _ => trivial
#align set.pairwise_top Set.pairwise_top

protected theorem Subsingleton.pairwise (h : s.Subsingleton) (r : Î± â†’ Î± â†’ Prop) : s.Pairwise r :=
  fun _x hx _y hy hne => (hne (h hx hy)).elim
#align set.subsingleton.pairwise Set.Subsingleton.pairwise

@[simp]
theorem pairwise_empty (r : Î± â†’ Î± â†’ Prop) : (âˆ… : Set Î±).Pairwise r :=
  subsingleton_empty.pairwise r
#align set.pairwise_empty Set.pairwise_empty

@[simp]
theorem pairwise_singleton (a : Î±) (r : Î± â†’ Î± â†’ Prop) : Set.Pairwise {a} r :=
  subsingleton_singleton.pairwise r
#align set.pairwise_singleton Set.pairwise_singleton

theorem pairwise_iff_of_refl [IsRefl Î± r] : s.Pairwise r â†” âˆ€ â¦ƒaâ¦„, a âˆˆ s â†’ âˆ€ â¦ƒbâ¦„, b âˆˆ s â†’ r a b :=
  forallâ‚„_congr fun _ _ _ _ => or_iff_not_imp_left.symm.trans <| or_iff_right_of_imp of_eq
#align set.pairwise_iff_of_refl Set.pairwise_iff_of_refl

alias âŸ¨Pairwise.of_refl, _âŸ© := pairwise_iff_of_refl
#align set.pairwise.of_refl Set.Pairwise.of_refl

theorem Nonempty.pairwise_iff_exists_forall [IsEquiv Î± r] {s : Set Î¹} (hs : s.Nonempty) :
    s.Pairwise (r on f) â†” âˆƒ z, âˆ€ x âˆˆ s, r (f x) z := by
  constructor
  -- âŠ¢ Set.Pairwise s (r on f) â†’ âˆƒ z, âˆ€ (x : Î¹), x âˆˆ s â†’ r (f x) z
  Â· rcases hs with âŸ¨y, hyâŸ©
    -- âŠ¢ Set.Pairwise s (r on f) â†’ âˆƒ z, âˆ€ (x : Î¹), x âˆˆ s â†’ r (f x) z
    refine' fun H => âŸ¨f y, fun x hx => _âŸ©
    -- âŠ¢ r (f x) (f y)
    rcases eq_or_ne x y with (rfl | hne)
    -- âŠ¢ r (f x) (f x)
    Â· apply IsRefl.refl
      -- ğŸ‰ no goals
    Â· exact H hx hy hne
      -- ğŸ‰ no goals
  Â· rintro âŸ¨z, hzâŸ© x hx y hy _
    -- âŠ¢ (r on f) x y
    exact @IsTrans.trans Î± r _ (f x) z (f y) (hz _ hx) (IsSymm.symm _ _ <| hz _ hy)
    -- ğŸ‰ no goals
#align set.nonempty.pairwise_iff_exists_forall Set.Nonempty.pairwise_iff_exists_forall

/-- For a nonempty set `s`, a function `f` takes pairwise equal values on `s` if and only if
for some `z` in the codomain, `f` takes value `z` on all `x âˆˆ s`. See also
`Set.pairwise_eq_iff_exists_eq` for a version that assumes `[Nonempty Î¹]` instead of
`Set.Nonempty s`. -/
theorem Nonempty.pairwise_eq_iff_exists_eq {s : Set Î±} (hs : s.Nonempty) {f : Î± â†’ Î¹} :
    (s.Pairwise fun x y => f x = f y) â†” âˆƒ z, âˆ€ x âˆˆ s, f x = z :=
  hs.pairwise_iff_exists_forall
#align set.nonempty.pairwise_eq_iff_exists_eq Set.Nonempty.pairwise_eq_iff_exists_eq

theorem pairwise_iff_exists_forall [Nonempty Î¹] (s : Set Î±) (f : Î± â†’ Î¹) {r : Î¹ â†’ Î¹ â†’ Prop}
    [IsEquiv Î¹ r] : s.Pairwise (r on f) â†” âˆƒ z, âˆ€ x âˆˆ s, r (f x) z := by
  rcases s.eq_empty_or_nonempty with (rfl | hne)
  -- âŠ¢ Set.Pairwise âˆ… (r on f) â†” âˆƒ z, âˆ€ (x : Î±), x âˆˆ âˆ… â†’ r (f x) z
  Â· simp
    -- ğŸ‰ no goals
  Â· exact hne.pairwise_iff_exists_forall
    -- ğŸ‰ no goals
#align set.pairwise_iff_exists_forall Set.pairwise_iff_exists_forall

/-- A function `f : Î± â†’ Î¹` with nonempty codomain takes pairwise equal values on a set `s` if and
only if for some `z` in the codomain, `f` takes value `z` on all `x âˆˆ s`. See also
`Set.Nonempty.pairwise_eq_iff_exists_eq` for a version that assumes `Set.Nonempty s` instead of
`[Nonempty Î¹]`. -/
theorem pairwise_eq_iff_exists_eq [Nonempty Î¹] (s : Set Î±) (f : Î± â†’ Î¹) :
    (s.Pairwise fun x y => f x = f y) â†” âˆƒ z, âˆ€ x âˆˆ s, f x = z :=
  pairwise_iff_exists_forall s f
#align set.pairwise_eq_iff_exists_eq Set.pairwise_eq_iff_exists_eq

theorem pairwise_union :
  (s âˆª t).Pairwise r â†”
    s.Pairwise r âˆ§ t.Pairwise r âˆ§ âˆ€ a âˆˆ s, âˆ€ b âˆˆ t, a â‰  b â†’ r a b âˆ§ r b a := by
  simp only [Set.Pairwise, mem_union, or_imp, forall_and]
  -- âŠ¢ (((âˆ€ (x : Î±), x âˆˆ s â†’ âˆ€ (x_1 : Î±), x_1 âˆˆ s â†’ x â‰  x_1 â†’ r x x_1) âˆ§ âˆ€ (x : Î±), â€¦
  exact
    âŸ¨fun H => âŸ¨H.1.1, H.2.2, H.2.1, fun x hx y hy hne => H.1.2 y hy x hx hne.symmâŸ©, fun H =>
      âŸ¨âŸ¨H.1, fun x hx y hy hne => H.2.2.2 y hy x hx hne.symmâŸ©, H.2.2.1, H.2.1âŸ©âŸ©
#align set.pairwise_union Set.pairwise_union

theorem pairwise_union_of_symmetric (hr : Symmetric r) :
    (s âˆª t).Pairwise r â†” s.Pairwise r âˆ§ t.Pairwise r âˆ§ âˆ€ a âˆˆ s, âˆ€ b âˆˆ t, a â‰  b â†’ r a b :=
  pairwise_union.trans <| by simp only [hr.iff, and_self_iff]
                             -- ğŸ‰ no goals
#align set.pairwise_union_of_symmetric Set.pairwise_union_of_symmetric

theorem pairwise_insert :
    (insert a s).Pairwise r â†” s.Pairwise r âˆ§ âˆ€ b âˆˆ s, a â‰  b â†’ r a b âˆ§ r b a := by
  simp only [insert_eq, pairwise_union, pairwise_singleton, true_and_iff, mem_singleton_iff,
    forall_eq]
#align set.pairwise_insert Set.pairwise_insert

theorem pairwise_insert_of_not_mem (ha : a âˆ‰ s) :
    (insert a s).Pairwise r â†” s.Pairwise r âˆ§ âˆ€ b âˆˆ s, r a b âˆ§ r b a :=
  pairwise_insert.trans <|
    and_congr_right' <| forallâ‚‚_congr fun b hb => by simp [(ne_of_mem_of_not_mem hb ha).symm]
                                                     -- ğŸ‰ no goals
#align set.pairwise_insert_of_not_mem Set.pairwise_insert_of_not_mem

protected theorem Pairwise.insert (hs : s.Pairwise r) (h : âˆ€ b âˆˆ s, a â‰  b â†’ r a b âˆ§ r b a) :
    (insert a s).Pairwise r :=
  pairwise_insert.2 âŸ¨hs, hâŸ©
#align set.pairwise.insert Set.Pairwise.insert

theorem Pairwise.insert_of_not_mem (ha : a âˆ‰ s) (hs : s.Pairwise r) (h : âˆ€ b âˆˆ s, r a b âˆ§ r b a) :
    (insert a s).Pairwise r :=
  (pairwise_insert_of_not_mem ha).2 âŸ¨hs, hâŸ©
#align set.pairwise.insert_of_not_mem Set.Pairwise.insert_of_not_mem

theorem pairwise_insert_of_symmetric (hr : Symmetric r) :
    (insert a s).Pairwise r â†” s.Pairwise r âˆ§ âˆ€ b âˆˆ s, a â‰  b â†’ r a b := by
  simp only [pairwise_insert, hr.iff a, and_self_iff]
  -- ğŸ‰ no goals
#align set.pairwise_insert_of_symmetric Set.pairwise_insert_of_symmetric

theorem pairwise_insert_of_symmetric_of_not_mem (hr : Symmetric r) (ha : a âˆ‰ s) :
    (insert a s).Pairwise r â†” s.Pairwise r âˆ§ âˆ€ b âˆˆ s, r a b := by
  simp only [pairwise_insert_of_not_mem ha, hr.iff a, and_self_iff]
  -- ğŸ‰ no goals
#align set.pairwise_insert_of_symmetric_of_not_mem Set.pairwise_insert_of_symmetric_of_not_mem

theorem Pairwise.insert_of_symmetric (hs : s.Pairwise r) (hr : Symmetric r)
    (h : âˆ€ b âˆˆ s, a â‰  b â†’ r a b) : (insert a s).Pairwise r :=
  (pairwise_insert_of_symmetric hr).2 âŸ¨hs, hâŸ©
#align set.pairwise.insert_of_symmetric Set.Pairwise.insert_of_symmetric

theorem Pairwise.insert_of_symmetric_of_not_mem (hs : s.Pairwise r) (hr : Symmetric r) (ha : a âˆ‰ s)
    (h : âˆ€ b âˆˆ s, r a b) : (insert a s).Pairwise r :=
  (pairwise_insert_of_symmetric_of_not_mem hr ha).2 âŸ¨hs, hâŸ©
#align set.pairwise.insert_of_symmetric_of_not_mem Set.Pairwise.insert_of_symmetric_of_not_mem

theorem pairwise_pair : Set.Pairwise {a, b} r â†” a â‰  b â†’ r a b âˆ§ r b a := by simp [pairwise_insert]
                                                                            -- ğŸ‰ no goals
#align set.pairwise_pair Set.pairwise_pair

theorem pairwise_pair_of_symmetric (hr : Symmetric r) : Set.Pairwise {a, b} r â†” a â‰  b â†’ r a b := by
  simp [pairwise_insert_of_symmetric hr]
  -- ğŸ‰ no goals
#align set.pairwise_pair_of_symmetric Set.pairwise_pair_of_symmetric

theorem pairwise_univ : (univ : Set Î±).Pairwise r â†” Pairwise r := by
  simp only [Set.Pairwise, Pairwise, mem_univ, forall_const]
  -- ğŸ‰ no goals
#align set.pairwise_univ Set.pairwise_univ

@[simp]
theorem pairwise_bot_iff : s.Pairwise (âŠ¥ : Î± â†’ Î± â†’ Prop) â†” (s : Set Î±).Subsingleton :=
  âŸ¨fun h _a ha _b hb => h.eq ha hb id, fun h => h.pairwise _âŸ©
#align set.pairwise_bot_iff Set.pairwise_bot_iff

alias âŸ¨Pairwise.subsingleton, _âŸ© := pairwise_bot_iff
#align set.pairwise.subsingleton Set.Pairwise.subsingleton

theorem InjOn.pairwise_image {s : Set Î¹} (h : s.InjOn f) :
    (f '' s).Pairwise r â†” s.Pairwise (r on f) := by
  simp (config := { contextual := true }) [h.eq_iff, Set.Pairwise]
  -- ğŸ‰ no goals
#align set.inj_on.pairwise_image Set.InjOn.pairwise_image

end Set

end Pairwise

theorem pairwise_subtype_iff_pairwise_set (s : Set Î±) (r : Î± â†’ Î± â†’ Prop) :
    (Pairwise fun (x : s) (y : s) => r x y) â†” s.Pairwise r := by
  simp only [Pairwise, Set.Pairwise, SetCoe.forall, Ne.def, Subtype.ext_iff, Subtype.coe_mk]
  -- ğŸ‰ no goals
#align pairwise_subtype_iff_pairwise_set pairwise_subtype_iff_pairwise_set

alias âŸ¨Pairwise.set_of_subtype, Set.Pairwise.subtypeâŸ© := pairwise_subtype_iff_pairwise_set
#align pairwise.set_of_subtype Pairwise.set_of_subtype
#align set.pairwise.subtype Set.Pairwise.subtype

namespace Set

section PartialOrderBot

variable [PartialOrder Î±] [OrderBot Î±] {s t : Set Î¹} {f g : Î¹ â†’ Î±}

/-- A set is `PairwiseDisjoint` under `f`, if the images of any distinct two elements under `f`
are disjoint.

`s.Pairwise Disjoint` is (definitionally) the same as `s.PairwiseDisjoint id`. We prefer the latter
in order to allow dot notation on `Set.PairwiseDisjoint`, even though the former unfolds more
nicely. -/
def PairwiseDisjoint (s : Set Î¹) (f : Î¹ â†’ Î±) : Prop :=
  s.Pairwise (Disjoint on f)
#align set.pairwise_disjoint Set.PairwiseDisjoint

theorem PairwiseDisjoint.subset (ht : t.PairwiseDisjoint f) (h : s âŠ† t) : s.PairwiseDisjoint f :=
  Pairwise.mono h ht
#align set.pairwise_disjoint.subset Set.PairwiseDisjoint.subset

theorem PairwiseDisjoint.mono_on (hs : s.PairwiseDisjoint f) (h : âˆ€ â¦ƒiâ¦„, i âˆˆ s â†’ g i â‰¤ f i) :
    s.PairwiseDisjoint g := fun _a ha _b hb hab => (hs ha hb hab).mono (h ha) (h hb)
#align set.pairwise_disjoint.mono_on Set.PairwiseDisjoint.mono_on

theorem PairwiseDisjoint.mono (hs : s.PairwiseDisjoint f) (h : g â‰¤ f) : s.PairwiseDisjoint g :=
  hs.mono_on fun i _ => h i
#align set.pairwise_disjoint.mono Set.PairwiseDisjoint.mono

@[simp]
theorem pairwiseDisjoint_empty : (âˆ… : Set Î¹).PairwiseDisjoint f :=
  pairwise_empty _
#align set.pairwise_disjoint_empty Set.pairwiseDisjoint_empty

@[simp]
theorem pairwiseDisjoint_singleton (i : Î¹) (f : Î¹ â†’ Î±) : PairwiseDisjoint {i} f :=
  pairwise_singleton i _
#align set.pairwise_disjoint_singleton Set.pairwiseDisjoint_singleton

theorem pairwiseDisjoint_insert {i : Î¹} :
    (insert i s).PairwiseDisjoint f â†”
      s.PairwiseDisjoint f âˆ§ âˆ€ j âˆˆ s, i â‰  j â†’ Disjoint (f i) (f j) :=
  pairwise_insert_of_symmetric <| symmetric_disjoint.comap f
#align set.pairwise_disjoint_insert Set.pairwiseDisjoint_insert

theorem pairwiseDisjoint_insert_of_not_mem {i : Î¹} (hi : i âˆ‰ s) :
    (insert i s).PairwiseDisjoint f â†” s.PairwiseDisjoint f âˆ§ âˆ€ j âˆˆ s, Disjoint (f i) (f j) :=
  pairwise_insert_of_symmetric_of_not_mem (symmetric_disjoint.comap f) hi
#align set.pairwise_disjoint_insert_of_not_mem Set.pairwiseDisjoint_insert_of_not_mem

protected theorem PairwiseDisjoint.insert (hs : s.PairwiseDisjoint f) {i : Î¹}
    (h : âˆ€ j âˆˆ s, i â‰  j â†’ Disjoint (f i) (f j)) : (insert i s).PairwiseDisjoint f :=
  pairwiseDisjoint_insert.2 âŸ¨hs, hâŸ©
#align set.pairwise_disjoint.insert Set.PairwiseDisjoint.insert

theorem PairwiseDisjoint.insert_of_not_mem (hs : s.PairwiseDisjoint f) {i : Î¹} (hi : i âˆ‰ s)
    (h : âˆ€ j âˆˆ s, Disjoint (f i) (f j)) : (insert i s).PairwiseDisjoint f :=
  (pairwiseDisjoint_insert_of_not_mem hi).2 âŸ¨hs, hâŸ©
#align set.pairwise_disjoint.insert_of_not_mem Set.PairwiseDisjoint.insert_of_not_mem

theorem PairwiseDisjoint.image_of_le (hs : s.PairwiseDisjoint f) {g : Î¹ â†’ Î¹} (hg : f âˆ˜ g â‰¤ f) :
    (g '' s).PairwiseDisjoint f := by
  rintro _ âŸ¨a, ha, rflâŸ© _ âŸ¨b, hb, rflâŸ© h
  -- âŠ¢ (Disjoint on f) (g a) (g b)
  exact (hs ha hb <| ne_of_apply_ne _ h).mono (hg a) (hg b)
  -- ğŸ‰ no goals
#align set.pairwise_disjoint.image_of_le Set.PairwiseDisjoint.image_of_le

theorem InjOn.pairwiseDisjoint_image {g : Î¹' â†’ Î¹} {s : Set Î¹'} (h : s.InjOn g) :
    (g '' s).PairwiseDisjoint f â†” s.PairwiseDisjoint (f âˆ˜ g) :=
  h.pairwise_image
#align set.inj_on.pairwise_disjoint_image Set.InjOn.pairwiseDisjoint_image

theorem PairwiseDisjoint.range (g : s â†’ Î¹) (hg : âˆ€ i : s, f (g i) â‰¤ f i)
    (ht : s.PairwiseDisjoint f) : (range g).PairwiseDisjoint f := by
  rintro _ âŸ¨x, rflâŸ© _ âŸ¨y, rflâŸ© hxy
  -- âŠ¢ (Disjoint on f) (g x) (g y)
  exact ((ht x.2 y.2) fun h => hxy <| congr_arg g <| Subtype.ext h).mono (hg x) (hg y)
  -- ğŸ‰ no goals
#align set.pairwise_disjoint.range Set.PairwiseDisjoint.range

theorem pairwiseDisjoint_union :
    (s âˆª t).PairwiseDisjoint f â†”
      s.PairwiseDisjoint f âˆ§
        t.PairwiseDisjoint f âˆ§ âˆ€ â¦ƒiâ¦„, i âˆˆ s â†’ âˆ€ â¦ƒjâ¦„, j âˆˆ t â†’ i â‰  j â†’ Disjoint (f i) (f j) :=
  pairwise_union_of_symmetric <| symmetric_disjoint.comap f
#align set.pairwise_disjoint_union Set.pairwiseDisjoint_union

theorem PairwiseDisjoint.union (hs : s.PairwiseDisjoint f) (ht : t.PairwiseDisjoint f)
    (h : âˆ€ â¦ƒiâ¦„, i âˆˆ s â†’ âˆ€ â¦ƒjâ¦„, j âˆˆ t â†’ i â‰  j â†’ Disjoint (f i) (f j)) : (s âˆª t).PairwiseDisjoint f :=
  pairwiseDisjoint_union.2 âŸ¨hs, ht, hâŸ©
#align set.pairwise_disjoint.union Set.PairwiseDisjoint.union

-- classical
theorem PairwiseDisjoint.elim (hs : s.PairwiseDisjoint f) {i j : Î¹} (hi : i âˆˆ s) (hj : j âˆˆ s)
    (h : Â¬Disjoint (f i) (f j)) : i = j :=
  hs.eq hi hj h
#align set.pairwise_disjoint.elim Set.PairwiseDisjoint.elim

end PartialOrderBot

section SemilatticeInfBot

variable [SemilatticeInf Î±] [OrderBot Î±] {s t : Set Î¹} {f g : Î¹ â†’ Î±}

-- classical
theorem PairwiseDisjoint.elim' (hs : s.PairwiseDisjoint f) {i j : Î¹} (hi : i âˆˆ s) (hj : j âˆˆ s)
    (h : f i âŠ“ f j â‰  âŠ¥) : i = j :=
  (hs.elim hi hj) fun hij => h hij.eq_bot
#align set.pairwise_disjoint.elim' Set.PairwiseDisjoint.elim'

theorem PairwiseDisjoint.eq_of_le (hs : s.PairwiseDisjoint f) {i j : Î¹} (hi : i âˆˆ s) (hj : j âˆˆ s)
    (hf : f i â‰  âŠ¥) (hij : f i â‰¤ f j) : i = j :=
  (hs.elim' hi hj) fun h => hf <| (inf_of_le_left hij).symm.trans h
#align set.pairwise_disjoint.eq_of_le Set.PairwiseDisjoint.eq_of_le

end SemilatticeInfBot

/-! ### Pairwise disjoint set of sets -/

variable {s : Set Î¹} {t : Set Î¹'}

theorem pairwiseDisjoint_range_singleton :
    (range (singleton : Î¹ â†’ Set Î¹)).PairwiseDisjoint id := by
  rintro _ âŸ¨a, rflâŸ© _ âŸ¨b, rflâŸ© h
  -- âŠ¢ (Disjoint on id) {a} {b}
  exact disjoint_singleton.2 (ne_of_apply_ne _ h)
  -- ğŸ‰ no goals
#align set.pairwise_disjoint_range_singleton Set.pairwiseDisjoint_range_singleton

theorem pairwiseDisjoint_fiber (f : Î¹ â†’ Î±) (s : Set Î±) : s.PairwiseDisjoint fun a => f â»Â¹' {a} :=
  fun _a _ _b _ h => disjoint_iff_inf_le.mpr fun _i âŸ¨hia, hibâŸ© => h <| (Eq.symm hia).trans hib
#align set.pairwise_disjoint_fiber Set.pairwiseDisjoint_fiber

-- classical
theorem PairwiseDisjoint.elim_set {s : Set Î¹} {f : Î¹ â†’ Set Î±} (hs : s.PairwiseDisjoint f) {i j : Î¹}
    (hi : i âˆˆ s) (hj : j âˆˆ s) (a : Î±) (hai : a âˆˆ f i) (haj : a âˆˆ f j) : i = j :=
  hs.elim hi hj <| not_disjoint_iff.2 âŸ¨a, hai, hajâŸ©
#align set.pairwise_disjoint.elim_set Set.PairwiseDisjoint.elim_set

theorem PairwiseDisjoint.prod {f : Î¹ â†’ Set Î±} {g : Î¹' â†’ Set Î²} (hs : s.PairwiseDisjoint f)
    (ht : t.PairwiseDisjoint g) :
    (s Ã—Ë¢ t : Set (Î¹ Ã— Î¹')).PairwiseDisjoint fun i => f i.1 Ã—Ë¢ g i.2 :=
  fun âŸ¨_, _âŸ© âŸ¨hi, hi'âŸ© âŸ¨_, _âŸ© âŸ¨hj, hj'âŸ© hij =>
  disjoint_left.2 fun âŸ¨_, _âŸ© âŸ¨hai, hbiâŸ© âŸ¨haj, hbjâŸ© =>
    hij <| Prod.ext (hs.elim_set hi hj _ hai haj) <| ht.elim_set hi' hj' _ hbi hbj
#align set.pairwise_disjoint.prod Set.PairwiseDisjoint.prod

theorem pairwiseDisjoint_pi {Î¹' Î± : Î¹ â†’ Type*} {s : âˆ€ i, Set (Î¹' i)} {f : âˆ€ i, Î¹' i â†’ Set (Î± i)}
    (hs : âˆ€ i, (s i).PairwiseDisjoint (f i)) :
    ((univ : Set Î¹).pi s).PairwiseDisjoint fun I => (univ : Set Î¹).pi fun i => f _ (I i) :=
  fun _ hI _ hJ hIJ =>
  disjoint_left.2 fun a haI haJ =>
    hIJ <|
      funext fun i =>
        (hs i).elim_set (hI i trivial) (hJ i trivial) (a i) (haI i trivial) (haJ i trivial)
#align set.pairwise_disjoint_pi Set.pairwiseDisjoint_pi

/-- The partial images of a binary function `f` whose partial evaluations are injective are pairwise
disjoint iff `f` is injective . -/
theorem pairwiseDisjoint_image_right_iff {f : Î± â†’ Î² â†’ Î³} {s : Set Î±} {t : Set Î²}
    (hf : âˆ€ a âˆˆ s, Injective (f a)) :
    (s.PairwiseDisjoint fun a => f a '' t) â†” (s Ã—Ë¢ t).InjOn fun p => f p.1 p.2 := by
  refine' âŸ¨fun hs x hx y hy (h : f _ _ = _) => _, fun hs x hx y hy h => _âŸ©
  -- âŠ¢ x = y
  Â· suffices x.1 = y.1 by exact Prod.ext this (hf _ hx.1 <| h.trans <| by rw [this])
    -- âŠ¢ x.fst = y.fst
    refine' hs.elim hx.1 hy.1 (not_disjoint_iff.2 âŸ¨_, mem_image_of_mem _ hx.2, _âŸ©)
    -- âŠ¢ f x.fst x.snd âˆˆ f y.fst '' t
    rw [h]
    -- âŠ¢ (fun p => f p.fst p.snd) y âˆˆ f y.fst '' t
    exact mem_image_of_mem _ hy.2
    -- ğŸ‰ no goals
  Â· refine' disjoint_iff_inf_le.mpr _
    -- âŠ¢ (fun a => f a '' t) x âŠ“ (fun a => f a '' t) y â‰¤ âŠ¥
    rintro _ âŸ¨âŸ¨a, ha, habâŸ©, b, hb, rflâŸ©
    -- âŠ¢ f y b âˆˆ âŠ¥
    exact h (congr_arg Prod.fst <| hs (mk_mem_prod hx ha) (mk_mem_prod hy hb) hab)
    -- ğŸ‰ no goals
#align set.pairwise_disjoint_image_right_iff Set.pairwiseDisjoint_image_right_iff

/-- The partial images of a binary function `f` whose partial evaluations are injective are pairwise
disjoint iff `f` is injective . -/
theorem pairwiseDisjoint_image_left_iff {f : Î± â†’ Î² â†’ Î³} {s : Set Î±} {t : Set Î²}
    (hf : âˆ€ b âˆˆ t, Injective fun a => f a b) :
    (t.PairwiseDisjoint fun b => (fun a => f a b) '' s) â†” (s Ã—Ë¢ t).InjOn fun p => f p.1 p.2 := by
  refine' âŸ¨fun ht x hx y hy (h : f _ _ = _) => _, fun ht x hx y hy h => _âŸ©
  -- âŠ¢ x = y
  Â· suffices x.2 = y.2 by exact Prod.ext (hf _ hx.2 <| h.trans <| by rw [this]) this
    -- âŠ¢ x.snd = y.snd
    refine' ht.elim hx.2 hy.2 (not_disjoint_iff.2 âŸ¨_, mem_image_of_mem _ hx.1, _âŸ©)
    -- âŠ¢ f x.fst x.snd âˆˆ (fun a => f a y.snd) '' s
    rw [h]
    -- âŠ¢ (fun p => f p.fst p.snd) y âˆˆ (fun a => f a y.snd) '' s
    exact mem_image_of_mem _ hy.1
    -- ğŸ‰ no goals
  Â· refine' disjoint_iff_inf_le.mpr _
    -- âŠ¢ (fun b => (fun a => f a b) '' s) x âŠ“ (fun b => (fun a => f a b) '' s) y â‰¤ âŠ¥
    rintro _ âŸ¨âŸ¨a, ha, habâŸ©, b, hb, rflâŸ©
    -- âŠ¢ (fun a => f a y) b âˆˆ âŠ¥
    exact h (congr_arg Prod.snd <| ht (mk_mem_prod ha hx) (mk_mem_prod hb hy) hab)
    -- ğŸ‰ no goals
#align set.pairwise_disjoint_image_left_iff Set.pairwiseDisjoint_image_left_iff

lemma exists_ne_mem_inter_of_not_pairwiseDisjoint
    {f : Î¹ â†’ Set Î±} (h : Â¬ s.PairwiseDisjoint f) :
    âˆƒ i âˆˆ s, âˆƒ j âˆˆ s, âˆƒ (_hij : i â‰  j) (x : Î±), x âˆˆ f i âˆ© f j := by
  change Â¬ âˆ€ i, i âˆˆ s â†’ âˆ€ j, j âˆˆ s â†’ i â‰  j â†’ âˆ€ t, t â‰¤ f i â†’ t â‰¤ f j â†’ t â‰¤ âŠ¥ at h
  -- âŠ¢ âˆƒ i, i âˆˆ s âˆ§ âˆƒ j, j âˆˆ s âˆ§ âˆƒ _hij x, x âˆˆ f i âˆ© f j
  simp only [not_forall] at h
  -- âŠ¢ âˆƒ i, i âˆˆ s âˆ§ âˆƒ j, j âˆˆ s âˆ§ âˆƒ _hij x, x âˆˆ f i âˆ© f j
  obtain âŸ¨i, hi, j, hj, h_ne, t, hfi, hfj, htâŸ© := h
  -- âŠ¢ âˆƒ i, i âˆˆ s âˆ§ âˆƒ j, j âˆˆ s âˆ§ âˆƒ _hij x, x âˆˆ f i âˆ© f j
  replace ht : t.Nonempty := by
    rwa [le_bot_iff, bot_eq_empty, â† Ne.def, â† nonempty_iff_ne_empty] at ht
  obtain âŸ¨x, hxâŸ© := ht
  -- âŠ¢ âˆƒ i, i âˆˆ s âˆ§ âˆƒ j, j âˆˆ s âˆ§ âˆƒ _hij x, x âˆˆ f i âˆ© f j
  exact âŸ¨i, hi, j, hj, h_ne, x, hfi hx, hfj hxâŸ©
  -- ğŸ‰ no goals

lemma exists_lt_mem_inter_of_not_pairwiseDisjoint [LinearOrder Î¹]
    {f : Î¹ â†’ Set Î±} (h : Â¬ s.PairwiseDisjoint f) :
    âˆƒ i âˆˆ s, âˆƒ j âˆˆ s, âˆƒ (_hij : i < j) (x : Î±), x âˆˆ f i âˆ© f j := by
  obtain âŸ¨i, hi, j, hj, hne, x, hxâ‚, hxâ‚‚âŸ© := exists_ne_mem_inter_of_not_pairwiseDisjoint h
  -- âŠ¢ âˆƒ i, i âˆˆ s âˆ§ âˆƒ j, j âˆˆ s âˆ§ âˆƒ _hij x, x âˆˆ f i âˆ© f j
  cases' lt_or_lt_iff_ne.mpr hne with h_lt h_lt
  -- âŠ¢ âˆƒ i, i âˆˆ s âˆ§ âˆƒ j, j âˆˆ s âˆ§ âˆƒ _hij x, x âˆˆ f i âˆ© f j
  Â· exact âŸ¨i, hi, j, hj, h_lt, x, hxâ‚, hxâ‚‚âŸ©
    -- ğŸ‰ no goals
  Â· exact âŸ¨j, hj, i, hi, h_lt, x, hxâ‚‚, hxâ‚âŸ©
    -- ğŸ‰ no goals

end Set

lemma exists_ne_mem_inter_of_not_pairwise_disjoint
    {f : Î¹ â†’ Set Î±} (h : Â¬ Pairwise (Disjoint on f)) :
    âˆƒ (i j : Î¹) (_hij : i â‰  j) (x : Î±), x âˆˆ f i âˆ© f j := by
  rw [â† pairwise_univ] at h
  -- âŠ¢ âˆƒ i j _hij x, x âˆˆ f i âˆ© f j
  obtain âŸ¨i, _hi, j, _hj, hâŸ© := exists_ne_mem_inter_of_not_pairwiseDisjoint h
  -- âŠ¢ âˆƒ i j _hij x, x âˆˆ f i âˆ© f j
  exact âŸ¨i, j, hâŸ©
  -- ğŸ‰ no goals

lemma exists_lt_mem_inter_of_not_pairwise_disjoint [LinearOrder Î¹]
    {f : Î¹ â†’ Set Î±} (h : Â¬ Pairwise (Disjoint on f)) :
    âˆƒ (i j : Î¹) (_ : i < j) (x : Î±), x âˆˆ f i âˆ© f j := by
  rw [â† pairwise_univ] at h
  -- âŠ¢ âˆƒ i j x x, x âˆˆ f i âˆ© f j
  obtain âŸ¨i, _hi, j, _hj, hâŸ© := exists_lt_mem_inter_of_not_pairwiseDisjoint h
  -- âŠ¢ âˆƒ i j x x, x âˆˆ f i âˆ© f j
  exact âŸ¨i, j, hâŸ©
  -- ğŸ‰ no goals

theorem pairwise_disjoint_fiber (f : Î¹ â†’ Î±) : Pairwise (Disjoint on fun a : Î± => f â»Â¹' {a}) :=
  pairwise_univ.1 <| Set.pairwiseDisjoint_fiber f univ
#align pairwise_disjoint_fiber pairwise_disjoint_fiber
