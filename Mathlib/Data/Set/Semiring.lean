/-
Copyright (c) 2020 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import Mathlib.Algebra.Order.Kleene
import Mathlib.Data.Set.Pointwise.SMul

#align_import data.set.semiring from "leanprover-community/mathlib"@"62e8311c791f02c47451bf14aa2501048e7c2f33"

/-!
# Sets as a semiring under union

This file defines `SetSemiring Î±`, an alias of `Set Î±`, which we endow with `âˆª` as addition and
pointwise `*` as multiplication. If `Î±` is a (commutative) monoid, `SetSemiring Î±` is a
(commutative) semiring.
-/


open Function Set

open Pointwise

variable {Î± Î² : Type*}

-- Porting note: mathlib3 uses `deriving Inhabited, PartialOrder, OrderBot`
/-- An alias for `Set Î±`, which has a semiring structure given by `âˆª` as "addition" and pointwise
  multiplication `*` as "multiplication". -/
def SetSemiring (Î± : Type*) : Type _ :=
  Set Î±
#align set_semiring SetSemiring

noncomputable instance (Î± : Type*) : Inhabited (SetSemiring Î±) :=
  (inferInstance : Inhabited (Set _))

instance (Î± : Type*) : PartialOrder (SetSemiring Î±) :=
  (inferInstance : PartialOrder (Set _))

instance (Î± : Type*) : OrderBot (SetSemiring Î±) :=
  (inferInstance : OrderBot (Set _))

/-- The identity function `Set Î± â†’ SetSemiring Î±`. -/
protected def Set.up : Set Î± â‰ƒ SetSemiring Î± :=
  Equiv.refl _
#align set.up Set.up

namespace SetSemiring

/-- The identity function `SetSemiring Î± â†’ Set Î±`. -/
protected def down : SetSemiring Î± â‰ƒ Set Î± :=
  Equiv.refl _
#align set_semiring.down SetSemiring.down

--Porting note: new, since dot notation doesn't work
open SetSemiring (down)
open Set (up)

--Porting note: dot notation no longer works
@[simp]
protected theorem down_up (s : Set Î±) : SetSemiring.down (Set.up s) = s :=
  rfl
#align set_semiring.down_up SetSemiring.down_up

--Porting note: dot notation no longer works
@[simp]
protected theorem up_down (s : SetSemiring Î±) : Set.up (SetSemiring.down s) = s :=
  rfl
#align set_semiring.up_down SetSemiring.up_down

-- TODO: These lemmas are not tagged `simp` because `Set.le_eq_subset` simplifies the LHS
--Porting note: dot notation no longer works
theorem up_le_up {s t : Set Î±} : Set.up s â‰¤ Set.up t â†” s âŠ† t :=
  Iff.rfl
#align set_semiring.up_le_up SetSemiring.up_le_up

--Porting note: dot notation no longer works
theorem up_lt_up {s t : Set Î±} : Set.up s < Set.up t â†” s âŠ‚ t :=
  Iff.rfl
#align set_semiring.up_lt_up SetSemiring.up_lt_up

--Porting note: dot notation no longer works
@[simp]
theorem down_subset_down {s t : SetSemiring Î±} : SetSemiring.down s âŠ† SetSemiring.down t â†” s â‰¤ t :=
  Iff.rfl
#align set_semiring.down_subset_down SetSemiring.down_subset_down

--Porting note: dot notation no longer works
@[simp]
theorem down_ssubset_down {s t : SetSemiring Î±} : SetSemiring.down s âŠ‚ SetSemiring.down t â†” s < t :=
  Iff.rfl
#align set_semiring.down_ssubset_down SetSemiring.down_ssubset_down

--Porting note: dot notation no longer works
instance : AddCommMonoid (SetSemiring Î±) where
  add s t := Set.up (SetSemiring.down s âˆª SetSemiring.down t)
  zero := Set.up (âˆ… : Set Î±)
  add_assoc := union_assoc
  zero_add := empty_union
  add_zero := union_empty
  add_comm := union_comm

theorem zero_def : (0 : SetSemiring Î±) = Set.up âˆ… :=
  rfl
#align set_semiring.zero_def SetSemiring.zero_def

@[simp]
theorem down_zero : down (0 : SetSemiring Î±) = âˆ… :=
  rfl
#align set_semiring.down_zero SetSemiring.down_zero

@[simp]
theorem _root_.Set.up_empty : Set.up (âˆ… : Set Î±) = 0 :=
  rfl
#align set.up_empty Set.up_empty

theorem add_def (s t : SetSemiring Î±) : s + t = up (down s âˆª down t) :=
  rfl
#align set_semiring.add_def SetSemiring.add_def

@[simp]
theorem down_add (s t : SetSemiring Î±) : down (s + t) = down s âˆª down t :=
  rfl
#align set_semiring.down_add SetSemiring.down_add

@[simp]
theorem _root_.Set.up_union (s t : Set Î±) : up (s âˆª t) = up s + up t :=
  rfl
#align set.up_union Set.up_union

/- Since addition on `SetSemiring` is commutative (it is set union), there is no need
to also have the instance `CovariantClass (SetSemiring Î±) (SetSemiring Î±) (swap (+)) (â‰¤)`. -/
instance covariantClass_add : CovariantClass (SetSemiring Î±) (SetSemiring Î±) (Â· + Â·) (Â· â‰¤ Â·) :=
  âŸ¨fun _ _ _ => union_subset_union_right _âŸ©
#align set_semiring.covariant_class_add SetSemiring.covariantClass_add

section Mul

variable [Mul Î±]

--Porting note: dot notation no longer works
instance : NonUnitalNonAssocSemiring (SetSemiring Î±) :=
  { (inferInstance : AddCommMonoid (SetSemiring Î±)) with
    mul := fun s t => Set.up (image2 (Â· * Â·) (SetSemiring.down s) (SetSemiring.down t))
    zero_mul := fun _ => empty_mul
    mul_zero := fun _ => mul_empty
    left_distrib := fun _ _ _ => mul_union
    right_distrib := fun _ _ _ => union_mul }

-- TODO: port
theorem mul_def (s t : SetSemiring Î±) : s * t = up (down s * down t) :=
  rfl
#align set_semiring.mul_def SetSemiring.mul_def

@[simp]
theorem down_mul (s t : SetSemiring Î±) : down (s * t) = down s * down t :=
  rfl
#align set_semiring.down_mul SetSemiring.down_mul

@[simp]
theorem _root_.Set.up_mul (s t : Set Î±) : up (s * t) = up s * up t :=
  rfl
#align set.up_mul Set.up_mul

instance : NoZeroDivisors (SetSemiring Î±) :=
  âŸ¨fun {a b} ab =>
    a.eq_empty_or_nonempty.imp_right fun ha =>
      b.eq_empty_or_nonempty.resolve_right fun hb =>
        Nonempty.ne_empty âŸ¨_, mul_mem_mul ha.some_mem hb.some_memâŸ© abâŸ©

instance covariantClass_mul_left :
    CovariantClass (SetSemiring Î±) (SetSemiring Î±) (Â· * Â·) (Â· â‰¤ Â·) :=
  âŸ¨fun _ _ _ => mul_subset_mul_leftâŸ©
#align set_semiring.covariant_class_mul_left SetSemiring.covariantClass_mul_left

instance covariantClass_mul_right :
    CovariantClass (SetSemiring Î±) (SetSemiring Î±) (swap (Â· * Â·)) (Â· â‰¤ Â·) :=
  âŸ¨fun _ _ _ => mul_subset_mul_rightâŸ©
#align set_semiring.covariant_class_mul_right SetSemiring.covariantClass_mul_right

end Mul


section One

variable [One Î±]

instance : One (SetSemiring Î±) where one := Set.up (1 : Set Î±)

theorem one_def : (1 : SetSemiring Î±) = Set.up 1 :=
  rfl
#align set_semiring.one_def SetSemiring.one_def

@[simp]
theorem down_one : down (1 : SetSemiring Î±) = 1 :=
  rfl
#align set_semiring.down_one SetSemiring.down_one

@[simp]
theorem _root_.Set.up_one : up (1 : Set Î±) = 1 :=
  rfl
#align set.up_one Set.up_one

end One

instance [MulOneClass Î±] : NonAssocSemiring (SetSemiring Î±) :=
  { (inferInstance : NonUnitalNonAssocSemiring (SetSemiring Î±)),
    Set.mulOneClass with
    one := 1
    mul := (Â· * Â·) }

instance [Semigroup Î±] : NonUnitalSemiring (SetSemiring Î±) :=
  { (inferInstance : NonUnitalNonAssocSemiring (SetSemiring Î±)), Set.semigroup with }

instance [Monoid Î±] : IdemSemiring (SetSemiring Î±) :=
  { (inferInstance : NonAssocSemiring (SetSemiring Î±)),
    (inferInstance : NonUnitalSemiring (SetSemiring Î±)),
    (inferInstance : CompleteBooleanAlgebra (Set Î±)) with }

instance [CommSemigroup Î±] : NonUnitalCommSemiring (SetSemiring Î±) :=
  { (inferInstance : NonUnitalSemiring (SetSemiring Î±)), Set.commSemigroup with }

instance [CommMonoid Î±] : IdemCommSemiring (SetSemiring Î±) :=
  { (inferInstance : IdemSemiring (SetSemiring Î±)),
    (inferInstance : CommMonoid (Set Î±)) with }

instance [CommMonoid Î±] : CommMonoid (SetSemiring Î±) :=
  { (inferInstance : Monoid (SetSemiring Î±)), Set.commSemigroup with }

instance [CommMonoid Î±] : CanonicallyOrderedCommSemiring (SetSemiring Î±) :=
  { (inferInstance : Semiring (SetSemiring Î±)), (inferInstance : CommMonoid (SetSemiring Î±)),
    (inferInstance : PartialOrder (SetSemiring Î±)), (inferInstance : OrderBot (SetSemiring Î±)),
    (inferInstance : NoZeroDivisors (SetSemiring Î±)) with
    add_le_add_left := fun _ _ => add_le_add_left
    exists_add_of_le := fun {_ b} ab => âŸ¨b, (union_eq_right_iff_subset.2 ab).symmâŸ©
    le_self_add := subset_union_left }

/-- The image of a set under a multiplicative homomorphism is a ring homomorphism
with respect to the pointwise operations on sets. -/
def imageHom [MulOneClass Î±] [MulOneClass Î²] (f : Î± â†’* Î²) : SetSemiring Î± â†’+* SetSemiring Î²
    where
  toFun s := up (image f (down s))
  map_zero' := image_empty _
  map_one' := by
    dsimp only  -- porting note: structures do not do this automatically any more
    -- âŠ¢ â†‘up (â†‘f '' â†‘down 1) = 1
    rw [down_one, image_one, map_one, singleton_one, up_one]
    -- ðŸŽ‰ no goals
  map_add' := image_union _
  map_mul' _ _ := image_mul f
#align set_semiring.image_hom SetSemiring.imageHom

lemma imageHom_def [MulOneClass Î±] [MulOneClass Î²] (f : Î± â†’* Î²) (s : SetSemiring Î±) :
    imageHom f s = up (image f (down s)) :=
  rfl
#align set_semiring.image_hom_def SetSemiring.imageHom_def

@[simp]
lemma down_imageHom [MulOneClass Î±] [MulOneClass Î²] (f : Î± â†’* Î²) (s : SetSemiring Î±) :
    down (imageHom f s) = f '' down s :=
  rfl
#align set_semiring.down_image_hom SetSemiring.down_imageHom

@[simp]
lemma _root_.Set.up_image [MulOneClass Î±] [MulOneClass Î²] (f : Î± â†’* Î²) (s : Set Î±) :
    up (f '' s) = imageHom f (up s) :=
  rfl
#align set.up_image Set.up_image

end SetSemiring
