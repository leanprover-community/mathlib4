/-
Copyright (c) 2014 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Andrew Zipperer, Haitao Zhang, Minchao Wu, Yury Kudryashov
-/
import Mathlib.Data.Set.Prod
import Mathlib.Logic.Function.Conjugate

#align_import data.set.function from "leanprover-community/mathlib"@"996b0ff959da753a555053a480f36e5f264d4207"

/-!
# Functions over sets

## Main definitions

### Predicate

* `Set.EqOn f‚ÇÅ f‚ÇÇ s` : functions `f‚ÇÅ` and `f‚ÇÇ` are equal at every point of `s`;
* `Set.MapsTo f s t` : `f` sends every point of `s` to a point of `t`;
* `Set.InjOn f s` : restriction of `f` to `s` is injective;
* `Set.SurjOn f s t` : every point in `s` has a preimage in `s`;
* `Set.BijOn f s t` : `f` is a bijection between `s` and `t`;
* `Set.LeftInvOn f' f s` : for every `x ‚àà s` we have `f' (f x) = x`;
* `Set.RightInvOn f' f t` : for every `y ‚àà t` we have `f (f' y) = y`;
* `Set.InvOn f' f s t` : `f'` is a two-side inverse of `f` on `s` and `t`, i.e.
  we have `Set.LeftInvOn f' f s` and `Set.RightInvOn f' f t`.

### Functions

* `Set.restrict f s` : restrict the domain of `f` to the set `s`;
* `Set.codRestrict f s h` : given `h : ‚àÄ x, f x ‚àà s`, restrict the codomain of `f` to the set `s`;
* `Set.MapsTo.restrict f s t h`: given `h : MapsTo f s t`, restrict the domain of `f` to `s`
  and the codomain to `t`.
-/

variable {Œ± Œ≤ Œ≥ : Type*} {Œπ : Sort*} {œÄ : Œ± ‚Üí Type*}

open Equiv Equiv.Perm Function

namespace Set

/-! ### Restrict -/


/-- Restrict domain of a function `f` to a set `s`. Same as `Subtype.restrict` but this version
takes an argument `‚Ü•s` instead of `Subtype s`. -/
def restrict (s : Set Œ±) (f : ‚àÄ a : Œ±, œÄ a) : ‚àÄ a : s, œÄ a := fun x => f x

theorem restrict_eq (f : Œ± ‚Üí Œ≤) (s : Set Œ±) : s.restrict f = f ‚àò Subtype.val :=
  rfl

@[simp]
theorem restrict_apply (f : Œ± ‚Üí Œ≤) (s : Set Œ±) (x : s) : s.restrict f x = f x :=
  rfl

theorem restrict_eq_iff {f : ‚àÄ a, œÄ a} {s : Set Œ±} {g : ‚àÄ a : s, œÄ a} :
    restrict s f = g ‚Üî ‚àÄ (a) (ha : a ‚àà s), f a = g ‚ü®a, ha‚ü© :=
  funext_iff.trans Subtype.forall

theorem eq_restrict_iff {s : Set Œ±} {f : ‚àÄ a : s, œÄ a} {g : ‚àÄ a, œÄ a} :
    f = restrict s g ‚Üî ‚àÄ (a) (ha : a ‚àà s), f ‚ü®a, ha‚ü© = g a :=
  funext_iff.trans Subtype.forall

@[simp]
theorem range_restrict (f : Œ± ‚Üí Œ≤) (s : Set Œ±) : Set.range (s.restrict f) = f '' s :=
  (range_comp _ _).trans <| congr_arg ((¬∑ '' ¬∑) f) Subtype.range_coe

theorem image_restrict (f : Œ± ‚Üí Œ≤) (s t : Set Œ±) :
    s.restrict f '' (Subtype.val ‚Åª¬π' t) = f '' (t ‚à© s) := by
  rw [restrict_eq, image_comp, image_preimage_eq_inter_range, Subtype.range_coe]

@[simp]
theorem restrict_dite {s : Set Œ±} [‚àÄ x, Decidable (x ‚àà s)] (f : ‚àÄ a ‚àà s, Œ≤)
    (g : ‚àÄ (a) (_ : a ‚àâ s), Œ≤) :
    (s.restrict fun a => if h : a ‚àà s then f a h else g a h) = (fun a : s => f a a.2) :=
  funext fun a => dif_pos a.2

@[simp]
theorem restrict_dite_compl {s : Set Œ±} [‚àÄ x, Decidable (x ‚àà s)] (f : ‚àÄ a ‚àà s, Œ≤)
    (g : ‚àÄ (a) (_ : a ‚àâ s), Œ≤) :
    (s·∂ú.restrict fun a => if h : a ‚àà s then f a h else g a h) = (fun a : (s·∂ú : Set Œ±) => g a a.2) :=
  funext fun a => dif_neg a.2

@[simp]
theorem restrict_ite (f g : Œ± ‚Üí Œ≤) (s : Set Œ±) [‚àÄ x, Decidable (x ‚àà s)] :
    (s.restrict fun a => if a ‚àà s then f a else g a) = s.restrict f :=
  restrict_dite _ _

@[simp]
theorem restrict_ite_compl (f g : Œ± ‚Üí Œ≤) (s : Set Œ±) [‚àÄ x, Decidable (x ‚àà s)] :
    (s·∂ú.restrict fun a => if a ‚àà s then f a else g a) = s·∂ú.restrict g :=
  restrict_dite_compl _ _

@[simp]
theorem restrict_piecewise (f g : Œ± ‚Üí Œ≤) (s : Set Œ±) [‚àÄ x, Decidable (x ‚àà s)] :
    s.restrict (piecewise s f g) = s.restrict f :=
  restrict_ite _ _ _

@[simp]
theorem restrict_piecewise_compl (f g : Œ± ‚Üí Œ≤) (s : Set Œ±) [‚àÄ x, Decidable (x ‚àà s)] :
    s·∂ú.restrict (piecewise s f g) = s·∂ú.restrict g :=
  restrict_ite_compl _ _ _

theorem restrict_extend_range (f : Œ± ‚Üí Œ≤) (g : Œ± ‚Üí Œ≥) (g' : Œ≤ ‚Üí Œ≥) :
    (range f).restrict (extend f g g') = fun x => g x.coe_prop.choose := by
  classical
  exact restrict_dite _ _

@[simp]
theorem restrict_extend_compl_range (f : Œ± ‚Üí Œ≤) (g : Œ± ‚Üí Œ≥) (g' : Œ≤ ‚Üí Œ≥) :
    (range f)·∂ú.restrict (extend f g g') = g' ‚àò Subtype.val := by
  classical
  exact restrict_dite_compl _ _

theorem range_extend_subset (f : Œ± ‚Üí Œ≤) (g : Œ± ‚Üí Œ≥) (g' : Œ≤ ‚Üí Œ≥) :
    range (extend f g g') ‚äÜ range g ‚à™ g' '' (range f)·∂ú := by
  classical
  rintro _ ‚ü®y, rfl‚ü©
  rw [extend_def]
  split_ifs with h
  exacts [Or.inl (mem_range_self _), Or.inr (mem_image_of_mem _ h)]

theorem range_extend {f : Œ± ‚Üí Œ≤} (hf : Injective f) (g : Œ± ‚Üí Œ≥) (g' : Œ≤ ‚Üí Œ≥) :
    range (extend f g g') = range g ‚à™ g' '' (range f)·∂ú := by
  refine' (range_extend_subset _ _ _).antisymm _
  rintro z (‚ü®x, rfl‚ü© | ‚ü®y, hy, rfl‚ü©)
  exacts [‚ü®f x, hf.extend_apply _ _ _‚ü©, ‚ü®y, extend_apply' _ _ _ hy‚ü©]

/-- Restrict codomain of a function `f` to a set `s`. Same as `Subtype.coind` but this version
has codomain `‚Ü•s` instead of `Subtype s`. -/
def codRestrict (f : Œπ ‚Üí Œ±) (s : Set Œ±) (h : ‚àÄ x, f x ‚àà s) : Œπ ‚Üí s := fun x => ‚ü®f x, h x‚ü©

@[simp]
theorem val_codRestrict_apply (f : Œπ ‚Üí Œ±) (s : Set Œ±) (h : ‚àÄ x, f x ‚àà s) (x : Œπ) :
    (codRestrict f s h x : Œ±) = f x :=
  rfl

@[simp]
theorem restrict_comp_codRestrict {f : Œπ ‚Üí Œ±} {g : Œ± ‚Üí Œ≤} {b : Set Œ±} (h : ‚àÄ x, f x ‚àà b) :
    b.restrict g ‚àò b.codRestrict f h = g ‚àò f :=
  rfl

@[simp]
theorem injective_codRestrict {f : Œπ ‚Üí Œ±} {s : Set Œ±} (h : ‚àÄ x, f x ‚àà s) :
    Injective (codRestrict f s h) ‚Üî Injective f := by
  simp only [Injective, Subtype.ext_iff, val_codRestrict_apply]

alias ‚ü®_, _root_.Function.Injective.codRestrict‚ü© := injective_codRestrict

variable {s s‚ÇÅ s‚ÇÇ : Set Œ±} {t t‚ÇÅ t‚ÇÇ : Set Œ≤} {p : Set Œ≥} {f f‚ÇÅ f‚ÇÇ f‚ÇÉ : Œ± ‚Üí Œ≤} {g g‚ÇÅ g‚ÇÇ : Œ≤ ‚Üí Œ≥}
  {f' f‚ÇÅ' f‚ÇÇ' : Œ≤ ‚Üí Œ±} {g' : Œ≥ ‚Üí Œ≤} {a : Œ±} {b : Œ≤}

/-! ### Equality on a set -/


/-- Two functions `f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤` are equal on `s`
  if `f‚ÇÅ x = f‚ÇÇ x` for all `x ‚àà s`. -/
def EqOn (f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤) (s : Set Œ±) : Prop :=
  ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí f‚ÇÅ x = f‚ÇÇ x

@[simp]
theorem eqOn_empty (f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤) : EqOn f‚ÇÅ f‚ÇÇ ‚àÖ := fun _ => False.elim

@[simp]
theorem eqOn_singleton : Set.EqOn f‚ÇÅ f‚ÇÇ {a} ‚Üî f‚ÇÅ a = f‚ÇÇ a := by
  simp [Set.EqOn]

@[simp]
theorem restrict_eq_restrict_iff : restrict s f‚ÇÅ = restrict s f‚ÇÇ ‚Üî EqOn f‚ÇÅ f‚ÇÇ s :=
  restrict_eq_iff

@[symm]
theorem EqOn.symm (h : EqOn f‚ÇÅ f‚ÇÇ s) : EqOn f‚ÇÇ f‚ÇÅ s := fun _ hx => (h hx).symm

theorem eqOn_comm : EqOn f‚ÇÅ f‚ÇÇ s ‚Üî EqOn f‚ÇÇ f‚ÇÅ s :=
  ‚ü®EqOn.symm, EqOn.symm‚ü©

-- This can not be tagged as `@[refl]` with the current argument order.
-- See note below at `EqOn.trans`.
theorem eqOn_refl (f : Œ± ‚Üí Œ≤) (s : Set Œ±) : EqOn f f s := fun _ _ => rfl

-- Note: this was formerly tagged with `@[trans]`, and although the `trans` attribute accepted it
-- the `trans` tactic could not use it.
-- An update to the trans tactic coming in mathlib4#7014 will reject this attribute.
-- It can be restored by changing the argument order from `EqOn f‚ÇÅ f‚ÇÇ s` to `EqOn s f‚ÇÅ f‚ÇÇ`.
-- This change will be made separately: [zulip](https://leanprover.zulipchat.com/#narrow/stream/287929-mathlib4/topic/Reordering.20arguments.20of.20.60Set.2EEqOn.60/near/390467581).
theorem EqOn.trans (h‚ÇÅ : EqOn f‚ÇÅ f‚ÇÇ s) (h‚ÇÇ : EqOn f‚ÇÇ f‚ÇÉ s) : EqOn f‚ÇÅ f‚ÇÉ s := fun _ hx =>
  (h‚ÇÅ hx).trans (h‚ÇÇ hx)

theorem EqOn.image_eq (heq : EqOn f‚ÇÅ f‚ÇÇ s) : f‚ÇÅ '' s = f‚ÇÇ '' s :=
  image_congr heq

theorem EqOn.inter_preimage_eq (heq : EqOn f‚ÇÅ f‚ÇÇ s) (t : Set Œ≤) : s ‚à© f‚ÇÅ ‚Åª¬π' t = s ‚à© f‚ÇÇ ‚Åª¬π' t :=
  ext fun x => and_congr_right_iff.2 fun hx => by rw [mem_preimage, mem_preimage, heq hx]

theorem EqOn.mono (hs : s‚ÇÅ ‚äÜ s‚ÇÇ) (hf : EqOn f‚ÇÅ f‚ÇÇ s‚ÇÇ) : EqOn f‚ÇÅ f‚ÇÇ s‚ÇÅ := fun _ hx => hf (hs hx)

@[simp]
theorem eqOn_union : EqOn f‚ÇÅ f‚ÇÇ (s‚ÇÅ ‚à™ s‚ÇÇ) ‚Üî EqOn f‚ÇÅ f‚ÇÇ s‚ÇÅ ‚àß EqOn f‚ÇÅ f‚ÇÇ s‚ÇÇ :=
  ball_or_left

theorem EqOn.union (h‚ÇÅ : EqOn f‚ÇÅ f‚ÇÇ s‚ÇÅ) (h‚ÇÇ : EqOn f‚ÇÅ f‚ÇÇ s‚ÇÇ) : EqOn f‚ÇÅ f‚ÇÇ (s‚ÇÅ ‚à™ s‚ÇÇ) :=
  eqOn_union.2 ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©

theorem EqOn.comp_left (h : s.EqOn f‚ÇÅ f‚ÇÇ) : s.EqOn (g ‚àò f‚ÇÅ) (g ‚àò f‚ÇÇ) := fun _ ha =>
  congr_arg _ <| h ha

@[simp]
theorem eqOn_range {Œπ : Sort*} {f : Œπ ‚Üí Œ±} {g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œ≤} :
    EqOn g‚ÇÅ g‚ÇÇ (range f) ‚Üî g‚ÇÅ ‚àò f = g‚ÇÇ ‚àò f :=
  forall_range_iff.trans <| funext_iff.symm

alias ‚ü®EqOn.comp_eq, _‚ü© := eqOn_range

/-! ### Congruence lemmas -/


section Order

variable [Preorder Œ±] [Preorder Œ≤]

theorem _root_.MonotoneOn.congr (h‚ÇÅ : MonotoneOn f‚ÇÅ s) (h : s.EqOn f‚ÇÅ f‚ÇÇ) : MonotoneOn f‚ÇÇ s := by
  intro a ha b hb hab
  rw [‚Üê h ha, ‚Üê h hb]
  exact h‚ÇÅ ha hb hab

theorem _root_.AntitoneOn.congr (h‚ÇÅ : AntitoneOn f‚ÇÅ s) (h : s.EqOn f‚ÇÅ f‚ÇÇ) : AntitoneOn f‚ÇÇ s :=
  h‚ÇÅ.dual_right.congr h

theorem _root_.StrictMonoOn.congr (h‚ÇÅ : StrictMonoOn f‚ÇÅ s) (h : s.EqOn f‚ÇÅ f‚ÇÇ) :
    StrictMonoOn f‚ÇÇ s := by
  intro a ha b hb hab
  rw [‚Üê h ha, ‚Üê h hb]
  exact h‚ÇÅ ha hb hab

theorem _root_.StrictAntiOn.congr (h‚ÇÅ : StrictAntiOn f‚ÇÅ s) (h : s.EqOn f‚ÇÅ f‚ÇÇ) : StrictAntiOn f‚ÇÇ s :=
  h‚ÇÅ.dual_right.congr h

theorem EqOn.congr_monotoneOn (h : s.EqOn f‚ÇÅ f‚ÇÇ) : MonotoneOn f‚ÇÅ s ‚Üî MonotoneOn f‚ÇÇ s :=
  ‚ü®fun h‚ÇÅ => h‚ÇÅ.congr h, fun h‚ÇÇ => h‚ÇÇ.congr h.symm‚ü©

theorem EqOn.congr_antitoneOn (h : s.EqOn f‚ÇÅ f‚ÇÇ) : AntitoneOn f‚ÇÅ s ‚Üî AntitoneOn f‚ÇÇ s :=
  ‚ü®fun h‚ÇÅ => h‚ÇÅ.congr h, fun h‚ÇÇ => h‚ÇÇ.congr h.symm‚ü©

theorem EqOn.congr_strictMonoOn (h : s.EqOn f‚ÇÅ f‚ÇÇ) : StrictMonoOn f‚ÇÅ s ‚Üî StrictMonoOn f‚ÇÇ s :=
  ‚ü®fun h‚ÇÅ => h‚ÇÅ.congr h, fun h‚ÇÇ => h‚ÇÇ.congr h.symm‚ü©

theorem EqOn.congr_strictAntiOn (h : s.EqOn f‚ÇÅ f‚ÇÇ) : StrictAntiOn f‚ÇÅ s ‚Üî StrictAntiOn f‚ÇÇ s :=
  ‚ü®fun h‚ÇÅ => h‚ÇÅ.congr h, fun h‚ÇÇ => h‚ÇÇ.congr h.symm‚ü©

end Order

/-! ### Mono lemmas-/


section Mono

variable [Preorder Œ±] [Preorder Œ≤]

theorem _root_.MonotoneOn.mono (h : MonotoneOn f s) (h' : s‚ÇÇ ‚äÜ s) : MonotoneOn f s‚ÇÇ :=
  fun _ hx _ hy => h (h' hx) (h' hy)

theorem _root_.AntitoneOn.mono (h : AntitoneOn f s) (h' : s‚ÇÇ ‚äÜ s) : AntitoneOn f s‚ÇÇ :=
  fun _ hx _ hy => h (h' hx) (h' hy)

theorem _root_.StrictMonoOn.mono (h : StrictMonoOn f s) (h' : s‚ÇÇ ‚äÜ s) : StrictMonoOn f s‚ÇÇ :=
  fun _ hx _ hy => h (h' hx) (h' hy)

theorem _root_.StrictAntiOn.mono (h : StrictAntiOn f s) (h' : s‚ÇÇ ‚äÜ s) : StrictAntiOn f s‚ÇÇ :=
  fun _ hx _ hy => h (h' hx) (h' hy)

protected theorem _root_.MonotoneOn.monotone (h : MonotoneOn f s) :
    Monotone (f ‚àò Subtype.val : s ‚Üí Œ≤) :=
  fun x y hle => h x.coe_prop y.coe_prop hle

protected theorem _root_.AntitoneOn.monotone (h : AntitoneOn f s) :
    Antitone (f ‚àò Subtype.val : s ‚Üí Œ≤) :=
  fun x y hle => h x.coe_prop y.coe_prop hle

protected theorem _root_.StrictMonoOn.strictMono (h : StrictMonoOn f s) :
    StrictMono (f ‚àò Subtype.val : s ‚Üí Œ≤) :=
  fun x y hlt => h x.coe_prop y.coe_prop hlt

protected theorem _root_.StrictAntiOn.strictAnti (h : StrictAntiOn f s) :
    StrictAnti (f ‚àò Subtype.val : s ‚Üí Œ≤) :=
  fun x y hlt => h x.coe_prop y.coe_prop hlt

end Mono

/-! ### maps to -/


/-- `MapsTo f a b` means that the image of `a` is contained in `b`. -/
def MapsTo (f : Œ± ‚Üí Œ≤) (s : Set Œ±) (t : Set Œ≤) : Prop :=
  ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí f x ‚àà t

/-- Given a map `f` sending `s : Set Œ±` into `t : Set Œ≤`, restrict domain of `f` to `s`
and the codomain to `t`. Same as `Subtype.map`. -/
def MapsTo.restrict (f : Œ± ‚Üí Œ≤) (s : Set Œ±) (t : Set Œ≤) (h : MapsTo f s t) : s ‚Üí t :=
  Subtype.map f h

theorem MapsTo.restrict_commutes (f : Œ± ‚Üí Œ≤) (s : Set Œ±) (t : Set Œ≤) (h : MapsTo f s t) :
    Subtype.val ‚àò h.restrict f s t = f ‚àò Subtype.val :=
  rfl

@[simp]
theorem MapsTo.val_restrict_apply (h : MapsTo f s t) (x : s) : (h.restrict f s t x : Œ≤) = f x :=
  rfl

/-- Restricting the domain and then the codomain is the same as `MapsTo.restrict`. -/
@[simp]
theorem codRestrict_restrict (h : ‚àÄ x : s, f x ‚àà t) :
    codRestrict (s.restrict f) t h = MapsTo.restrict f s t fun x hx => h ‚ü®x, hx‚ü© :=
  rfl

/-- Reverse of `Set.codRestrict_restrict`. -/
theorem MapsTo.restrict_eq_codRestrict (h : MapsTo f s t) :
    h.restrict f s t = codRestrict (s.restrict f) t fun x => h x.2 :=
  rfl

theorem MapsTo.coe_restrict (h : Set.MapsTo f s t) :
    Subtype.val ‚àò h.restrict f s t = s.restrict f :=
  rfl

theorem MapsTo.range_restrict (f : Œ± ‚Üí Œ≤) (s : Set Œ±) (t : Set Œ≤) (h : MapsTo f s t) :
    range (h.restrict f s t) = Subtype.val ‚Åª¬π' (f '' s) :=
  Set.range_subtype_map f h

theorem mapsTo_iff_exists_map_subtype : MapsTo f s t ‚Üî ‚àÉ g : s ‚Üí t, ‚àÄ x : s, f x = g x :=
  ‚ü®fun h => ‚ü®h.restrict f s t, fun _ => rfl‚ü©, fun ‚ü®g, hg‚ü© x hx => by
    erw [hg ‚ü®x, hx‚ü©]
    apply Subtype.coe_prop‚ü©

theorem mapsTo' : MapsTo f s t ‚Üî f '' s ‚äÜ t :=
  image_subset_iff.symm

theorem mapsTo_prod_map_diagonal : MapsTo (Prod.map f f) (diagonal Œ±) (diagonal Œ≤) :=
  diagonal_subset_iff.2 <| fun _ => rfl

theorem MapsTo.subset_preimage {f : Œ± ‚Üí Œ≤} {s : Set Œ±} {t : Set Œ≤} (hf : MapsTo f s t) :
    s ‚äÜ f ‚Åª¬π' t :=
  hf

@[simp]
theorem mapsTo_singleton {x : Œ±} : MapsTo f {x} t ‚Üî f x ‚àà t :=
  singleton_subset_iff

theorem mapsTo_empty (f : Œ± ‚Üí Œ≤) (t : Set Œ≤) : MapsTo f ‚àÖ t :=
  empty_subset _

theorem MapsTo.image_subset (h : MapsTo f s t) : f '' s ‚äÜ t :=
  mapsTo'.1 h

theorem MapsTo.congr (h‚ÇÅ : MapsTo f‚ÇÅ s t) (h : EqOn f‚ÇÅ f‚ÇÇ s) : MapsTo f‚ÇÇ s t := fun _ hx =>
  h hx ‚ñ∏ h‚ÇÅ hx

theorem EqOn.comp_right (hg : t.EqOn g‚ÇÅ g‚ÇÇ) (hf : s.MapsTo f t) : s.EqOn (g‚ÇÅ ‚àò f) (g‚ÇÇ ‚àò f) :=
  fun _ ha => hg <| hf ha

theorem EqOn.mapsTo_iff (H : EqOn f‚ÇÅ f‚ÇÇ s) : MapsTo f‚ÇÅ s t ‚Üî MapsTo f‚ÇÇ s t :=
  ‚ü®fun h => h.congr H, fun h => h.congr H.symm‚ü©

theorem MapsTo.comp (h‚ÇÅ : MapsTo g t p) (h‚ÇÇ : MapsTo f s t) : MapsTo (g ‚àò f) s p := fun _ h =>
  h‚ÇÅ (h‚ÇÇ h)

theorem mapsTo_id (s : Set Œ±) : MapsTo id s s := fun _ => id

theorem MapsTo.iterate {f : Œ± ‚Üí Œ±} {s : Set Œ±} (h : MapsTo f s s) : ‚àÄ n, MapsTo f^[n] s s
  | 0 => fun _ => id
  | n + 1 => (MapsTo.iterate h n).comp h

theorem MapsTo.iterate_restrict {f : Œ± ‚Üí Œ±} {s : Set Œ±} (h : MapsTo f s s) (n : ‚Ñï) :
    (h.restrict f s s)^[n] = (h.iterate n).restrict _ _ _ := by
  funext x
  rw [Subtype.ext_iff, MapsTo.val_restrict_apply]
  induction' n with n ihn generalizing x
  ¬∑ rfl
  ¬∑ simp [Nat.iterate, ihn]

lemma mapsTo_of_subsingleton' [Subsingleton Œ≤] (f : Œ± ‚Üí Œ≤) (h : s.Nonempty ‚Üí t.Nonempty) :
    MapsTo f s t :=
  fun a ha ‚Ü¶ Subsingleton.mem_iff_nonempty.2 $ h ‚ü®a, ha‚ü©

lemma mapsTo_of_subsingleton [Subsingleton Œ±] (f : Œ± ‚Üí Œ±) (s : Set Œ±) : MapsTo f s s :=
  mapsTo_of_subsingleton' _ id

theorem MapsTo.mono (hf : MapsTo f s‚ÇÅ t‚ÇÅ) (hs : s‚ÇÇ ‚äÜ s‚ÇÅ) (ht : t‚ÇÅ ‚äÜ t‚ÇÇ) : MapsTo f s‚ÇÇ t‚ÇÇ :=
  fun _ hx => ht (hf <| hs hx)

theorem MapsTo.mono_left (hf : MapsTo f s‚ÇÅ t) (hs : s‚ÇÇ ‚äÜ s‚ÇÅ) : MapsTo f s‚ÇÇ t := fun _ hx =>
  hf (hs hx)

theorem MapsTo.mono_right (hf : MapsTo f s t‚ÇÅ) (ht : t‚ÇÅ ‚äÜ t‚ÇÇ) : MapsTo f s t‚ÇÇ := fun _ hx =>
  ht (hf hx)

theorem MapsTo.union_union (h‚ÇÅ : MapsTo f s‚ÇÅ t‚ÇÅ) (h‚ÇÇ : MapsTo f s‚ÇÇ t‚ÇÇ) :
    MapsTo f (s‚ÇÅ ‚à™ s‚ÇÇ) (t‚ÇÅ ‚à™ t‚ÇÇ) := fun _ hx =>
  hx.elim (fun hx => Or.inl <| h‚ÇÅ hx) fun hx => Or.inr <| h‚ÇÇ hx

theorem MapsTo.union (h‚ÇÅ : MapsTo f s‚ÇÅ t) (h‚ÇÇ : MapsTo f s‚ÇÇ t) : MapsTo f (s‚ÇÅ ‚à™ s‚ÇÇ) t :=
  union_self t ‚ñ∏ h‚ÇÅ.union_union h‚ÇÇ

@[simp]
theorem mapsTo_union : MapsTo f (s‚ÇÅ ‚à™ s‚ÇÇ) t ‚Üî MapsTo f s‚ÇÅ t ‚àß MapsTo f s‚ÇÇ t :=
  ‚ü®fun h =>
    ‚ü®h.mono (subset_union_left s‚ÇÅ s‚ÇÇ) (Subset.refl t),
      h.mono (subset_union_right s‚ÇÅ s‚ÇÇ) (Subset.refl t)‚ü©,
    fun h => h.1.union h.2‚ü©

theorem MapsTo.inter (h‚ÇÅ : MapsTo f s t‚ÇÅ) (h‚ÇÇ : MapsTo f s t‚ÇÇ) : MapsTo f s (t‚ÇÅ ‚à© t‚ÇÇ) := fun _ hx =>
  ‚ü®h‚ÇÅ hx, h‚ÇÇ hx‚ü©

theorem MapsTo.inter_inter (h‚ÇÅ : MapsTo f s‚ÇÅ t‚ÇÅ) (h‚ÇÇ : MapsTo f s‚ÇÇ t‚ÇÇ) :
    MapsTo f (s‚ÇÅ ‚à© s‚ÇÇ) (t‚ÇÅ ‚à© t‚ÇÇ) := fun _ hx => ‚ü®h‚ÇÅ hx.1, h‚ÇÇ hx.2‚ü©

@[simp]
theorem mapsTo_inter : MapsTo f s (t‚ÇÅ ‚à© t‚ÇÇ) ‚Üî MapsTo f s t‚ÇÅ ‚àß MapsTo f s t‚ÇÇ :=
  ‚ü®fun h =>
    ‚ü®h.mono (Subset.refl s) (inter_subset_left t‚ÇÅ t‚ÇÇ),
      h.mono (Subset.refl s) (inter_subset_right t‚ÇÅ t‚ÇÇ)‚ü©,
    fun h => h.1.inter h.2‚ü©

theorem mapsTo_univ (f : Œ± ‚Üí Œ≤) (s : Set Œ±) : MapsTo f s univ := fun _ _ => trivial

theorem mapsTo_image (f : Œ± ‚Üí Œ≤) (s : Set Œ±) : MapsTo f s (f '' s) := by rw [mapsTo']

theorem mapsTo_preimage (f : Œ± ‚Üí Œ≤) (t : Set Œ≤) : MapsTo f (f ‚Åª¬π' t) t :=
  Subset.refl _

theorem mapsTo_range (f : Œ± ‚Üí Œ≤) (s : Set Œ±) : MapsTo f s (range f) :=
  (mapsTo_image f s).mono (Subset.refl s) (image_subset_range _ _)

@[simp]
theorem maps_image_to (f : Œ± ‚Üí Œ≤) (g : Œ≥ ‚Üí Œ±) (s : Set Œ≥) (t : Set Œ≤) :
    MapsTo f (g '' s) t ‚Üî MapsTo (f ‚àò g) s t :=
  ‚ü®fun h c hc => h ‚ü®c, hc, rfl‚ü©, fun h _ ‚ü®_, hc‚ü© => hc.2 ‚ñ∏ h hc.1‚ü©

lemma MapsTo.comp_left (g : Œ≤ ‚Üí Œ≥) (hf : MapsTo f s t) : MapsTo (g ‚àò f) s (g '' t) :=
  fun x hx ‚Ü¶ ‚ü®f x, hf hx, rfl‚ü©

lemma MapsTo.comp_right {s : Set Œ≤} {t : Set Œ≥} (hg : MapsTo g s t) (f : Œ± ‚Üí Œ≤) :
    MapsTo (g ‚àò f) (f ‚Åª¬π' s) t := fun _ hx ‚Ü¶ hg hx

@[simp]
theorem maps_univ_to (f : Œ± ‚Üí Œ≤) (s : Set Œ≤) : MapsTo f univ s ‚Üî ‚àÄ a, f a ‚àà s :=
  ‚ü®fun h _ => h (mem_univ _), fun h x _ => h x‚ü©

@[simp]
theorem maps_range_to (f : Œ± ‚Üí Œ≤) (g : Œ≥ ‚Üí Œ±) (s : Set Œ≤) :
    MapsTo f (range g) s ‚Üî MapsTo (f ‚àò g) univ s := by rw [‚Üê image_univ, maps_image_to]

theorem surjective_mapsTo_image_restrict (f : Œ± ‚Üí Œ≤) (s : Set Œ±) :
    Surjective ((mapsTo_image f s).restrict f s (f '' s)) := fun ‚ü®_, x, hs, hxy‚ü© =>
  ‚ü®‚ü®x, hs‚ü©, Subtype.ext hxy‚ü©

theorem MapsTo.mem_iff (h : MapsTo f s t) (hc : MapsTo f s·∂ú t·∂ú) {x} : f x ‚àà t ‚Üî x ‚àà s :=
  ‚ü®fun ht => by_contra fun hs => hc hs ht, fun hx => h hx‚ü©

/-! ### Restriction onto preimage -/


section

variable (t f)

/-- The restriction of a function onto the preimage of a set. -/
@[simps!]
def restrictPreimage : f ‚Åª¬π' t ‚Üí t :=
  (Set.mapsTo_preimage f t).restrict _ _ _

theorem range_restrictPreimage : range (t.restrictPreimage f) = Subtype.val ‚Åª¬π' range f := by
  delta Set.restrictPreimage
  rw [MapsTo.range_restrict, Set.image_preimage_eq_inter_range, Set.preimage_inter,
    Subtype.coe_preimage_self, Set.univ_inter]

variable {f} {U : Œπ ‚Üí Set Œ≤}

lemma restrictPreimage_injective (hf : Injective f) : Injective (t.restrictPreimage f) :=
  fun _ _ e => Subtype.coe_injective <| hf <| Subtype.mk.inj e

lemma restrictPreimage_surjective (hf : Surjective f) : Surjective (t.restrictPreimage f) :=
  fun x => ‚ü®‚ü®_, ((hf x).choose_spec.symm ‚ñ∏ x.2 : _ ‚àà t)‚ü©, Subtype.ext (hf x).choose_spec‚ü©

lemma restrictPreimage_bijective (hf : Bijective f) : Bijective (t.restrictPreimage f) :=
  ‚ü®t.restrictPreimage_injective hf.1, t.restrictPreimage_surjective hf.2‚ü©

alias _root_.Function.Injective.restrictPreimage := Set.restrictPreimage_injective
alias _root_.Function.Surjective.restrictPreimage := Set.restrictPreimage_surjective
alias _root_.Function.Bijective.restrictPreimage := Set.restrictPreimage_bijective

end

/-! ### Injectivity on a set -/


/-- `f` is injective on `a` if the restriction of `f` to `a` is injective. -/
def InjOn (f : Œ± ‚Üí Œ≤) (s : Set Œ±) : Prop :=
  ‚àÄ ‚¶Éx‚ÇÅ : Œ±‚¶Ñ, x‚ÇÅ ‚àà s ‚Üí ‚àÄ ‚¶Éx‚ÇÇ : Œ±‚¶Ñ, x‚ÇÇ ‚àà s ‚Üí f x‚ÇÅ = f x‚ÇÇ ‚Üí x‚ÇÅ = x‚ÇÇ

theorem Subsingleton.injOn (hs : s.Subsingleton) (f : Œ± ‚Üí Œ≤) : InjOn f s := fun _ hx _ hy _ =>
  hs hx hy

@[simp]
theorem injOn_empty (f : Œ± ‚Üí Œ≤) : InjOn f ‚àÖ :=
  subsingleton_empty.injOn f
@[simp]
theorem injOn_singleton (f : Œ± ‚Üí Œ≤) (a : Œ±) : InjOn f {a} :=
  subsingleton_singleton.injOn f

theorem InjOn.eq_iff {x y} (h : InjOn f s) (hx : x ‚àà s) (hy : y ‚àà s) : f x = f y ‚Üî x = y :=
  ‚ü®h hx hy, fun h => h ‚ñ∏ rfl‚ü©

theorem InjOn.ne_iff {x y} (h : InjOn f s) (hx : x ‚àà s) (hy : y ‚àà s) : f x ‚â† f y ‚Üî x ‚â† y :=
  (h.eq_iff hx hy).not

alias ‚ü®_, InjOn.ne‚ü© := InjOn.ne_iff

theorem InjOn.congr (h‚ÇÅ : InjOn f‚ÇÅ s) (h : EqOn f‚ÇÅ f‚ÇÇ s) : InjOn f‚ÇÇ s := fun _ hx _ hy =>
  h hx ‚ñ∏ h hy ‚ñ∏ h‚ÇÅ hx hy

theorem EqOn.injOn_iff (H : EqOn f‚ÇÅ f‚ÇÇ s) : InjOn f‚ÇÅ s ‚Üî InjOn f‚ÇÇ s :=
  ‚ü®fun h => h.congr H, fun h => h.congr H.symm‚ü©

theorem InjOn.mono (h : s‚ÇÅ ‚äÜ s‚ÇÇ) (ht : InjOn f s‚ÇÇ) : InjOn f s‚ÇÅ := fun _ hx _ hy H =>
  ht (h hx) (h hy) H

theorem injOn_union (h : Disjoint s‚ÇÅ s‚ÇÇ) :
    InjOn f (s‚ÇÅ ‚à™ s‚ÇÇ) ‚Üî InjOn f s‚ÇÅ ‚àß InjOn f s‚ÇÇ ‚àß ‚àÄ x ‚àà s‚ÇÅ, ‚àÄ y ‚àà s‚ÇÇ, f x ‚â† f y := by
  refine' ‚ü®fun H => ‚ü®H.mono <| subset_union_left _ _, H.mono <| subset_union_right _ _, _‚ü©, _‚ü©
  ¬∑ intro x hx y hy hxy
    obtain rfl : x = y := H (Or.inl hx) (Or.inr hy) hxy
    exact h.le_bot ‚ü®hx, hy‚ü©
  ¬∑ rintro ‚ü®h‚ÇÅ, h‚ÇÇ, h‚ÇÅ‚ÇÇ‚ü©
    rintro x (hx | hx) y (hy | hy) hxy
    exacts [h‚ÇÅ hx hy hxy, (h‚ÇÅ‚ÇÇ _ hx _ hy hxy).elim, (h‚ÇÅ‚ÇÇ _ hy _ hx hxy.symm).elim, h‚ÇÇ hx hy hxy]

theorem injOn_insert {f : Œ± ‚Üí Œ≤} {s : Set Œ±} {a : Œ±} (has : a ‚àâ s) :
    Set.InjOn f (insert a s) ‚Üî Set.InjOn f s ‚àß f a ‚àâ f '' s := by
  have : Disjoint s {a} := disjoint_iff_inf_le.mpr fun x ‚ü®hxs, (hxa : x = a)‚ü© => has (hxa ‚ñ∏ hxs)
  rw [‚Üê union_singleton, injOn_union this]
  simp

theorem injective_iff_injOn_univ : Injective f ‚Üî InjOn f univ :=
  ‚ü®fun h _ _ _ _ hxy => h hxy, fun h _ _ heq => h trivial trivial heq‚ü©

theorem injOn_of_injective (h : Injective f) (s : Set Œ±) : InjOn f s := fun _ _ _ _ hxy => h hxy

alias _root_.Function.Injective.injOn := injOn_of_injective

-- A specialization of `injOn_of_injective` for `Subtype.val`.
theorem injOn_subtype_val {s : Set { x // p x }} : Set.InjOn Subtype.val s :=
  Subtype.coe_injective.injOn s

lemma injOn_id (s : Set Œ±) : InjOn id s := injective_id.injOn _

theorem InjOn.comp (hg : InjOn g t) (hf : InjOn f s) (h : MapsTo f s t) : InjOn (g ‚àò f) s :=
  fun _ hx _ hy heq => hf hx hy <| hg (h hx) (h hy) heq

lemma InjOn.iterate {f : Œ± ‚Üí Œ±} {s : Set Œ±} (h : InjOn f s) (hf : MapsTo f s s) :
    ‚àÄ n, InjOn f^[n] s
  | 0 => injOn_id _
  | (n + 1) => (h.iterate hf n).comp h hf

lemma injOn_of_subsingleton [Subsingleton Œ±] (f : Œ± ‚Üí Œ≤) (s : Set Œ±) : InjOn f s :=
  (injective_of_subsingleton _).injOn _

theorem _root_.Function.Injective.injOn_range (h : Injective (g ‚àò f)) : InjOn g (range f) := by
  rintro _ ‚ü®x, rfl‚ü© _ ‚ü®y, rfl‚ü© H
  exact congr_arg f (h H)

theorem injOn_iff_injective : InjOn f s ‚Üî Injective (s.restrict f) :=
  ‚ü®fun H a b h => Subtype.eq <| H a.2 b.2 h, fun H a as b bs h =>
    congr_arg Subtype.val <| @H ‚ü®a, as‚ü© ‚ü®b, bs‚ü© h‚ü©

alias ‚ü®InjOn.injective, _‚ü© := Set.injOn_iff_injective

theorem MapsTo.restrict_inj (h : MapsTo f s t) : Injective (h.restrict f s t) ‚Üî InjOn f s := by
  rw [h.restrict_eq_codRestrict, injective_codRestrict, injOn_iff_injective]

theorem exists_injOn_iff_injective [Nonempty Œ≤] :
    (‚àÉ f : Œ± ‚Üí Œ≤, InjOn f s) ‚Üî ‚àÉ f : s ‚Üí Œ≤, Injective f :=
  ‚ü®fun ‚ü®f, hf‚ü© => ‚ü®_, hf.injective‚ü©,
   fun ‚ü®f, hf‚ü© => by
    lift f to Œ± ‚Üí Œ≤ using trivial
    exact ‚ü®f, injOn_iff_injective.2 hf‚ü©‚ü©

theorem injOn_preimage {B : Set (Set Œ≤)} (hB : B ‚äÜ ùí´ range f) : InjOn (preimage f) B :=
  fun s hs t ht hst => (preimage_eq_preimage' (@hB s hs) (@hB t ht)).1 hst
-- porting note: is there a semi-implicit variable problem with `‚äÜ`?

theorem InjOn.mem_of_mem_image {x} (hf : InjOn f s) (hs : s‚ÇÅ ‚äÜ s) (h : x ‚àà s) (h‚ÇÅ : f x ‚àà f '' s‚ÇÅ) :
    x ‚àà s‚ÇÅ :=
  let ‚ü®_, h', Eq‚ü© := h‚ÇÅ
  hf (hs h') h Eq ‚ñ∏ h'

theorem InjOn.mem_image_iff {x} (hf : InjOn f s) (hs : s‚ÇÅ ‚äÜ s) (hx : x ‚àà s) :
    f x ‚àà f '' s‚ÇÅ ‚Üî x ‚àà s‚ÇÅ :=
  ‚ü®hf.mem_of_mem_image hs hx, mem_image_of_mem f‚ü©

theorem InjOn.preimage_image_inter (hf : InjOn f s) (hs : s‚ÇÅ ‚äÜ s) : f ‚Åª¬π' (f '' s‚ÇÅ) ‚à© s = s‚ÇÅ :=
  ext fun _ => ‚ü®fun ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© => hf.mem_of_mem_image hs h‚ÇÇ h‚ÇÅ, fun h => ‚ü®mem_image_of_mem _ h, hs h‚ü©‚ü©

theorem EqOn.cancel_left (h : s.EqOn (g ‚àò f‚ÇÅ) (g ‚àò f‚ÇÇ)) (hg : t.InjOn g) (hf‚ÇÅ : s.MapsTo f‚ÇÅ t)
    (hf‚ÇÇ : s.MapsTo f‚ÇÇ t) : s.EqOn f‚ÇÅ f‚ÇÇ := fun _ ha => hg (hf‚ÇÅ ha) (hf‚ÇÇ ha) (h ha)

theorem InjOn.cancel_left (hg : t.InjOn g) (hf‚ÇÅ : s.MapsTo f‚ÇÅ t) (hf‚ÇÇ : s.MapsTo f‚ÇÇ t) :
    s.EqOn (g ‚àò f‚ÇÅ) (g ‚àò f‚ÇÇ) ‚Üî s.EqOn f‚ÇÅ f‚ÇÇ :=
  ‚ü®fun h => h.cancel_left hg hf‚ÇÅ hf‚ÇÇ, EqOn.comp_left‚ü©

lemma InjOn.image_inter {s t u : Set Œ±} (hf : u.InjOn f) (hs : s ‚äÜ u) (ht : t ‚äÜ u) :
    f '' (s ‚à© t) = f '' s ‚à© f '' t := by
  apply Subset.antisymm (image_inter_subset _ _ _)
  intro x ‚ü®‚ü®y, ys, hy‚ü©, ‚ü®z, zt, hz‚ü©‚ü©
  have : y = z := by
    apply hf (hs ys) (ht zt)
    rwa [‚Üê hz] at hy
  rw [‚Üê this] at zt
  exact ‚ü®y, ‚ü®ys, zt‚ü©, hy‚ü©

theorem _root_.Disjoint.image {s t u : Set Œ±} {f : Œ± ‚Üí Œ≤} (h : Disjoint s t) (hf : u.InjOn f)
    (hs : s ‚äÜ u) (ht : t ‚äÜ u) : Disjoint (f '' s) (f '' t) := by
  rw [disjoint_iff_inter_eq_empty] at h ‚ä¢
  rw [‚Üê hf.image_inter hs ht, h, image_empty]

/-! ### Surjectivity on a set -/


/-- `f` is surjective from `a` to `b` if `b` is contained in the image of `a`. -/
def SurjOn (f : Œ± ‚Üí Œ≤) (s : Set Œ±) (t : Set Œ≤) : Prop :=
  t ‚äÜ f '' s

theorem SurjOn.subset_range (h : SurjOn f s t) : t ‚äÜ range f :=
  Subset.trans h <| image_subset_range f s

theorem surjOn_iff_exists_map_subtype :
    SurjOn f s t ‚Üî ‚àÉ (t' : Set Œ≤) (g : s ‚Üí t'), t ‚äÜ t' ‚àß Surjective g ‚àß ‚àÄ x : s, f x = g x :=
  ‚ü®fun h =>
    ‚ü®_, (mapsTo_image f s).restrict f s _, h, surjective_mapsTo_image_restrict _ _, fun _ => rfl‚ü©,
    fun ‚ü®t', g, htt', hg, hfg‚ü© y hy =>
    let ‚ü®x, hx‚ü© := hg ‚ü®y, htt' hy‚ü©
    ‚ü®x, x.2, by rw [hfg, hx, Subtype.coe_mk]‚ü©‚ü©

theorem surjOn_empty (f : Œ± ‚Üí Œ≤) (s : Set Œ±) : SurjOn f s ‚àÖ :=
  empty_subset _

@[simp] lemma surjOn_singleton : SurjOn f s {b} ‚Üî b ‚àà f '' s := singleton_subset_iff

theorem surjOn_image (f : Œ± ‚Üí Œ≤) (s : Set Œ±) : SurjOn f s (f '' s) :=
  Subset.rfl

theorem SurjOn.comap_nonempty (h : SurjOn f s t) (ht : t.Nonempty) : s.Nonempty :=
  (ht.mono h).of_image

theorem SurjOn.congr (h : SurjOn f‚ÇÅ s t) (H : EqOn f‚ÇÅ f‚ÇÇ s) : SurjOn f‚ÇÇ s t := by
  rwa [SurjOn, ‚Üê H.image_eq]

theorem EqOn.surjOn_iff (h : EqOn f‚ÇÅ f‚ÇÇ s) : SurjOn f‚ÇÅ s t ‚Üî SurjOn f‚ÇÇ s t :=
  ‚ü®fun H => H.congr h, fun H => H.congr h.symm‚ü©

theorem SurjOn.mono (hs : s‚ÇÅ ‚äÜ s‚ÇÇ) (ht : t‚ÇÅ ‚äÜ t‚ÇÇ) (hf : SurjOn f s‚ÇÅ t‚ÇÇ) : SurjOn f s‚ÇÇ t‚ÇÅ :=
  Subset.trans ht <| Subset.trans hf <| image_subset _ hs

theorem SurjOn.union (h‚ÇÅ : SurjOn f s t‚ÇÅ) (h‚ÇÇ : SurjOn f s t‚ÇÇ) : SurjOn f s (t‚ÇÅ ‚à™ t‚ÇÇ) := fun _ hx =>
  hx.elim (fun hx => h‚ÇÅ hx) fun hx => h‚ÇÇ hx

theorem SurjOn.union_union (h‚ÇÅ : SurjOn f s‚ÇÅ t‚ÇÅ) (h‚ÇÇ : SurjOn f s‚ÇÇ t‚ÇÇ) :
    SurjOn f (s‚ÇÅ ‚à™ s‚ÇÇ) (t‚ÇÅ ‚à™ t‚ÇÇ) :=
  (h‚ÇÅ.mono (subset_union_left _ _) (Subset.refl _)).union
    (h‚ÇÇ.mono (subset_union_right _ _) (Subset.refl _))

theorem SurjOn.inter_inter (h‚ÇÅ : SurjOn f s‚ÇÅ t‚ÇÅ) (h‚ÇÇ : SurjOn f s‚ÇÇ t‚ÇÇ) (h : InjOn f (s‚ÇÅ ‚à™ s‚ÇÇ)) :
    SurjOn f (s‚ÇÅ ‚à© s‚ÇÇ) (t‚ÇÅ ‚à© t‚ÇÇ) := by
  intro y hy
  rcases h‚ÇÅ hy.1 with ‚ü®x‚ÇÅ, hx‚ÇÅ, rfl‚ü©
  rcases h‚ÇÇ hy.2 with ‚ü®x‚ÇÇ, hx‚ÇÇ, heq‚ü©
  obtain rfl : x‚ÇÅ = x‚ÇÇ := h (Or.inl hx‚ÇÅ) (Or.inr hx‚ÇÇ) heq.symm
  exact mem_image_of_mem f ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©

theorem SurjOn.inter (h‚ÇÅ : SurjOn f s‚ÇÅ t) (h‚ÇÇ : SurjOn f s‚ÇÇ t) (h : InjOn f (s‚ÇÅ ‚à™ s‚ÇÇ)) :
    SurjOn f (s‚ÇÅ ‚à© s‚ÇÇ) t :=
  inter_self t ‚ñ∏ h‚ÇÅ.inter_inter h‚ÇÇ h

--porting note: Why does `simp` not call `refl` by itself?
lemma surjOn_id (s : Set Œ±) : SurjOn id s s := by simp [SurjOn, subset_rfl]

theorem SurjOn.comp (hg : SurjOn g t p) (hf : SurjOn f s t) : SurjOn (g ‚àò f) s p :=
  Subset.trans hg <| Subset.trans (image_subset g hf) <| image_comp g f s ‚ñ∏ Subset.refl _

lemma SurjOn.iterate {f : Œ± ‚Üí Œ±} {s : Set Œ±} (h : SurjOn f s s) : ‚àÄ n, SurjOn f^[n] s s
  | 0 => surjOn_id _
  | (n + 1) => (h.iterate n).comp h

lemma SurjOn.comp_left (hf : SurjOn f s t) (g : Œ≤ ‚Üí Œ≥) : SurjOn (g ‚àò f) s (g '' t) := by
  rw [SurjOn, image_comp g f]; exact image_subset _ hf

lemma SurjOn.comp_right {s : Set Œ≤} {t : Set Œ≥} (hf : Surjective f) (hg : SurjOn g s t) :
    SurjOn (g ‚àò f) (f ‚Åª¬π' s) t := by
  rwa [SurjOn, image_comp g f, image_preimage_eq _ hf]

lemma surjOn_of_subsingleton' [Subsingleton Œ≤] (f : Œ± ‚Üí Œ≤) (h : t.Nonempty ‚Üí s.Nonempty) :
    SurjOn f s t :=
  fun _ ha ‚Ü¶ Subsingleton.mem_iff_nonempty.2 $ (h ‚ü®_, ha‚ü©).image _

lemma surjOn_of_subsingleton [Subsingleton Œ±] (f : Œ± ‚Üí Œ±) (s : Set Œ±) : SurjOn f s s :=
  surjOn_of_subsingleton' _ id

theorem surjective_iff_surjOn_univ : Surjective f ‚Üî SurjOn f univ univ := by
  simp [Surjective, SurjOn, subset_def]

theorem surjOn_iff_surjective : SurjOn f s univ ‚Üî Surjective (s.restrict f) :=
  ‚ü®fun H b =>
    let ‚ü®a, as, e‚ü© := @H b trivial
    ‚ü®‚ü®a, as‚ü©, e‚ü©,
    fun H b _ =>
    let ‚ü®‚ü®a, as‚ü©, e‚ü© := H b
    ‚ü®a, as, e‚ü©‚ü©

@[simp]
theorem MapsTo.restrict_surjective_iff (h : MapsTo f s t) :
    Surjective (MapsTo.restrict _ _ _ h) ‚Üî SurjOn f s t := by
  refine ‚ü®fun h' b hb ‚Ü¶ ?_, fun h' ‚ü®b, hb‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®‚ü®a, ha‚ü©, ha'‚ü© := h' ‚ü®b, hb‚ü©
    replace ha' : f a = b := by simpa [Subtype.ext_iff] using ha'
    rw [‚Üê ha']
    exact mem_image_of_mem f ha
  ¬∑ obtain ‚ü®a, ha, rfl‚ü© := h' hb
    exact ‚ü®‚ü®a, ha‚ü©, rfl‚ü©

theorem SurjOn.image_eq_of_mapsTo (h‚ÇÅ : SurjOn f s t) (h‚ÇÇ : MapsTo f s t) : f '' s = t :=
  eq_of_subset_of_subset h‚ÇÇ.image_subset h‚ÇÅ

theorem image_eq_iff_surjOn_mapsTo : f '' s = t ‚Üî s.SurjOn f t ‚àß s.MapsTo f t := by
  refine' ‚ü®_, fun h => h.1.image_eq_of_mapsTo h.2‚ü©
  rintro rfl
  exact ‚ü®s.surjOn_image f, s.mapsTo_image f‚ü©

theorem SurjOn.mapsTo_compl (h : SurjOn f s t) (h' : Injective f) : MapsTo f s·∂ú t·∂ú :=
  fun _ hs ht =>
  let ‚ü®_, hx', HEq‚ü© := h ht
  hs <| h' HEq ‚ñ∏ hx'

theorem MapsTo.surjOn_compl (h : MapsTo f s t) (h' : Surjective f) : SurjOn f s·∂ú t·∂ú :=
  h'.forall.2 fun _ ht => (mem_image_of_mem _) fun hs => ht (h hs)

theorem EqOn.cancel_right (hf : s.EqOn (g‚ÇÅ ‚àò f) (g‚ÇÇ ‚àò f)) (hf' : s.SurjOn f t) : t.EqOn g‚ÇÅ g‚ÇÇ := by
  intro b hb
  obtain ‚ü®a, ha, rfl‚ü© := hf' hb
  exact hf ha

theorem SurjOn.cancel_right (hf : s.SurjOn f t) (hf' : s.MapsTo f t) :
    s.EqOn (g‚ÇÅ ‚àò f) (g‚ÇÇ ‚àò f) ‚Üî t.EqOn g‚ÇÅ g‚ÇÇ :=
  ‚ü®fun h => h.cancel_right hf, fun h => h.comp_right hf'‚ü©

theorem eqOn_comp_right_iff : s.EqOn (g‚ÇÅ ‚àò f) (g‚ÇÇ ‚àò f) ‚Üî (f '' s).EqOn g‚ÇÅ g‚ÇÇ :=
  (s.surjOn_image f).cancel_right <| s.mapsTo_image f

/-! ### Bijectivity -/


/-- `f` is bijective from `s` to `t` if `f` is injective on `s` and `f '' s = t`. -/
def BijOn (f : Œ± ‚Üí Œ≤) (s : Set Œ±) (t : Set Œ≤) : Prop :=
  MapsTo f s t ‚àß InjOn f s ‚àß SurjOn f s t

theorem BijOn.mapsTo (h : BijOn f s t) : MapsTo f s t :=
  h.left

theorem BijOn.injOn (h : BijOn f s t) : InjOn f s :=
  h.right.left

theorem BijOn.surjOn (h : BijOn f s t) : SurjOn f s t :=
  h.right.right

theorem BijOn.mk (h‚ÇÅ : MapsTo f s t) (h‚ÇÇ : InjOn f s) (h‚ÇÉ : SurjOn f s t) : BijOn f s t :=
  ‚ü®h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü©

theorem bijOn_empty (f : Œ± ‚Üí Œ≤) : BijOn f ‚àÖ ‚àÖ :=
  ‚ü®mapsTo_empty f ‚àÖ, injOn_empty f, surjOn_empty f ‚àÖ‚ü©

@[simp] lemma bijOn_singleton : BijOn f {a} {b} ‚Üî f a = b := by simp [BijOn, eq_comm]

theorem BijOn.inter_mapsTo (h‚ÇÅ : BijOn f s‚ÇÅ t‚ÇÅ) (h‚ÇÇ : MapsTo f s‚ÇÇ t‚ÇÇ) (h‚ÇÉ : s‚ÇÅ ‚à© f ‚Åª¬π' t‚ÇÇ ‚äÜ s‚ÇÇ) :
    BijOn f (s‚ÇÅ ‚à© s‚ÇÇ) (t‚ÇÅ ‚à© t‚ÇÇ) :=
  ‚ü®h‚ÇÅ.mapsTo.inter_inter h‚ÇÇ, h‚ÇÅ.injOn.mono <| inter_subset_left _ _, fun _ hy =>
    let ‚ü®x, hx, hxy‚ü© := h‚ÇÅ.surjOn hy.1
    ‚ü®x, ‚ü®hx, h‚ÇÉ ‚ü®hx, hxy.symm.subst hy.2‚ü©‚ü©, hxy‚ü©‚ü©

theorem MapsTo.inter_bijOn (h‚ÇÅ : MapsTo f s‚ÇÅ t‚ÇÅ) (h‚ÇÇ : BijOn f s‚ÇÇ t‚ÇÇ) (h‚ÇÉ : s‚ÇÇ ‚à© f ‚Åª¬π' t‚ÇÅ ‚äÜ s‚ÇÅ) :
    BijOn f (s‚ÇÅ ‚à© s‚ÇÇ) (t‚ÇÅ ‚à© t‚ÇÇ) :=
  inter_comm s‚ÇÇ s‚ÇÅ ‚ñ∏ inter_comm t‚ÇÇ t‚ÇÅ ‚ñ∏ h‚ÇÇ.inter_mapsTo h‚ÇÅ h‚ÇÉ

theorem BijOn.inter (h‚ÇÅ : BijOn f s‚ÇÅ t‚ÇÅ) (h‚ÇÇ : BijOn f s‚ÇÇ t‚ÇÇ) (h : InjOn f (s‚ÇÅ ‚à™ s‚ÇÇ)) :
    BijOn f (s‚ÇÅ ‚à© s‚ÇÇ) (t‚ÇÅ ‚à© t‚ÇÇ) :=
  ‚ü®h‚ÇÅ.mapsTo.inter_inter h‚ÇÇ.mapsTo, h‚ÇÅ.injOn.mono <| inter_subset_left _ _,
    h‚ÇÅ.surjOn.inter_inter h‚ÇÇ.surjOn h‚ü©

theorem BijOn.union (h‚ÇÅ : BijOn f s‚ÇÅ t‚ÇÅ) (h‚ÇÇ : BijOn f s‚ÇÇ t‚ÇÇ) (h : InjOn f (s‚ÇÅ ‚à™ s‚ÇÇ)) :
    BijOn f (s‚ÇÅ ‚à™ s‚ÇÇ) (t‚ÇÅ ‚à™ t‚ÇÇ) :=
  ‚ü®h‚ÇÅ.mapsTo.union_union h‚ÇÇ.mapsTo, h, h‚ÇÅ.surjOn.union_union h‚ÇÇ.surjOn‚ü©

theorem BijOn.subset_range (h : BijOn f s t) : t ‚äÜ range f :=
  h.surjOn.subset_range

theorem InjOn.bijOn_image (h : InjOn f s) : BijOn f s (f '' s) :=
  BijOn.mk (mapsTo_image f s) h (Subset.refl _)

theorem BijOn.congr (h‚ÇÅ : BijOn f‚ÇÅ s t) (h : EqOn f‚ÇÅ f‚ÇÇ s) : BijOn f‚ÇÇ s t :=
  BijOn.mk (h‚ÇÅ.mapsTo.congr h) (h‚ÇÅ.injOn.congr h) (h‚ÇÅ.surjOn.congr h)

theorem EqOn.bijOn_iff (H : EqOn f‚ÇÅ f‚ÇÇ s) : BijOn f‚ÇÅ s t ‚Üî BijOn f‚ÇÇ s t :=
  ‚ü®fun h => h.congr H, fun h => h.congr H.symm‚ü©

theorem BijOn.image_eq (h : BijOn f s t) : f '' s = t :=
  h.surjOn.image_eq_of_mapsTo h.mapsTo

lemma bijOn_id (s : Set Œ±) : BijOn id s s := ‚ü®s.mapsTo_id, s.injOn_id, s.surjOn_id‚ü©

theorem BijOn.comp (hg : BijOn g t p) (hf : BijOn f s t) : BijOn (g ‚àò f) s p :=
  BijOn.mk (hg.mapsTo.comp hf.mapsTo) (hg.injOn.comp hf.injOn hf.mapsTo) (hg.surjOn.comp hf.surjOn)

lemma BijOn.iterate {f : Œ± ‚Üí Œ±} {s : Set Œ±} (h : BijOn f s s) : ‚àÄ n, BijOn f^[n] s s
  | 0 => s.bijOn_id
  | (n + 1) => (h.iterate n).comp h

lemma bijOn_of_subsingleton' [Subsingleton Œ±] [Subsingleton Œ≤] (f : Œ± ‚Üí Œ≤)
    (h : s.Nonempty ‚Üî t.Nonempty) : BijOn f s t :=
  ‚ü®mapsTo_of_subsingleton' _ h.1, injOn_of_subsingleton _ _, surjOn_of_subsingleton' _ h.2‚ü©

lemma bijOn_of_subsingleton [Subsingleton Œ±] (f : Œ± ‚Üí Œ±) (s : Set Œ±) : BijOn f s s :=
  bijOn_of_subsingleton' _ Iff.rfl

theorem BijOn.bijective (h : BijOn f s t) : Bijective (h.mapsTo.restrict f s t) :=
  ‚ü®fun x y h' => Subtype.ext <| h.injOn x.2 y.2 <| Subtype.ext_iff.1 h', fun ‚ü®_, hy‚ü© =>
    let ‚ü®x, hx, hxy‚ü© := h.surjOn hy
    ‚ü®‚ü®x, hx‚ü©, Subtype.eq hxy‚ü©‚ü©

theorem bijective_iff_bijOn_univ : Bijective f ‚Üî BijOn f univ univ :=
  Iff.intro
    (fun h =>
      let ‚ü®inj, surj‚ü© := h
      ‚ü®mapsTo_univ f _, inj.injOn _, Iff.mp surjective_iff_surjOn_univ surj‚ü©)
    fun h =>
    let ‚ü®_map, inj, surj‚ü© := h
    ‚ü®Iff.mpr injective_iff_injOn_univ inj, Iff.mpr surjective_iff_surjOn_univ surj‚ü©

alias ‚ü®_root_.Function.Bijective.bijOn_univ, _‚ü© := bijective_iff_bijOn_univ

theorem BijOn.compl (hst : BijOn f s t) (hf : Bijective f) : BijOn f s·∂ú t·∂ú :=
  ‚ü®hst.surjOn.mapsTo_compl hf.1, hf.1.injOn _, hst.mapsTo.surjOn_compl hf.2‚ü©

/-! ### left inverse -/


/-- `g` is a left inverse to `f` on `a` means that `g (f x) = x` for all `x ‚àà a`. -/
def LeftInvOn (f' : Œ≤ ‚Üí Œ±) (f : Œ± ‚Üí Œ≤) (s : Set Œ±) : Prop :=
  ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí f' (f x) = x

theorem LeftInvOn.eqOn (h : LeftInvOn f' f s) : EqOn (f' ‚àò f) id s :=
  h

theorem LeftInvOn.eq (h : LeftInvOn f' f s) {x} (hx : x ‚àà s) : f' (f x) = x :=
  h hx

theorem LeftInvOn.congr_left (h‚ÇÅ : LeftInvOn f‚ÇÅ' f s) {t : Set Œ≤} (h‚ÇÅ' : MapsTo f s t)
    (heq : EqOn f‚ÇÅ' f‚ÇÇ' t) : LeftInvOn f‚ÇÇ' f s := fun _ hx => heq (h‚ÇÅ' hx) ‚ñ∏ h‚ÇÅ hx

theorem LeftInvOn.congr_right (h‚ÇÅ : LeftInvOn f‚ÇÅ' f‚ÇÅ s) (heq : EqOn f‚ÇÅ f‚ÇÇ s) : LeftInvOn f‚ÇÅ' f‚ÇÇ s :=
  fun _ hx => heq hx ‚ñ∏ h‚ÇÅ hx

theorem LeftInvOn.injOn (h : LeftInvOn f‚ÇÅ' f s) : InjOn f s := fun x‚ÇÅ h‚ÇÅ x‚ÇÇ h‚ÇÇ heq =>
  calc
    x‚ÇÅ = f‚ÇÅ' (f x‚ÇÅ) := Eq.symm <| h h‚ÇÅ
    _ = f‚ÇÅ' (f x‚ÇÇ) := congr_arg f‚ÇÅ' heq
    _ = x‚ÇÇ := h h‚ÇÇ

theorem LeftInvOn.surjOn (h : LeftInvOn f' f s) (hf : MapsTo f s t) : SurjOn f' t s := fun x hx =>
  ‚ü®f x, hf hx, h hx‚ü©

theorem LeftInvOn.mapsTo (h : LeftInvOn f' f s) (hf : SurjOn f s t) :
    MapsTo f' t s := fun y hy => by
  let ‚ü®x, hs, hx‚ü© := hf hy
  rwa [‚Üê hx, h hs]

lemma leftInvOn_id (s : Set Œ±) : LeftInvOn id id s := fun _ _ ‚Ü¶ rfl

theorem LeftInvOn.comp (hf' : LeftInvOn f' f s) (hg' : LeftInvOn g' g t) (hf : MapsTo f s t) :
    LeftInvOn (f' ‚àò g') (g ‚àò f) s := fun x h =>
  calc
    (f' ‚àò g') ((g ‚àò f) x) = f' (f x) := congr_arg f' (hg' (hf h))
    _ = x := hf' h

theorem LeftInvOn.mono (hf : LeftInvOn f' f s) (ht : s‚ÇÅ ‚äÜ s) : LeftInvOn f' f s‚ÇÅ := fun _ hx =>
  hf (ht hx)

theorem LeftInvOn.image_inter' (hf : LeftInvOn f' f s) : f '' (s‚ÇÅ ‚à© s) = f' ‚Åª¬π' s‚ÇÅ ‚à© f '' s := by
  apply Subset.antisymm
  ¬∑ rintro _ ‚ü®x, ‚ü®h‚ÇÅ, h‚ü©, rfl‚ü©
    exact ‚ü®by rwa [mem_preimage, hf h], mem_image_of_mem _ h‚ü©
  ¬∑ rintro _ ‚ü®h‚ÇÅ, ‚ü®x, h, rfl‚ü©‚ü©
    exact mem_image_of_mem _ ‚ü®by rwa [‚Üê hf h], h‚ü©

theorem LeftInvOn.image_inter (hf : LeftInvOn f' f s) :
    f '' (s‚ÇÅ ‚à© s) = f' ‚Åª¬π' (s‚ÇÅ ‚à© s) ‚à© f '' s := by
  rw [hf.image_inter']
  refine' Subset.antisymm _ (inter_subset_inter_left _ (preimage_mono <| inter_subset_left _ _))
  rintro _ ‚ü®h‚ÇÅ, x, hx, rfl‚ü©; exact ‚ü®‚ü®h‚ÇÅ, by rwa [hf hx]‚ü©, mem_image_of_mem _ hx‚ü©

theorem LeftInvOn.image_image (hf : LeftInvOn f' f s) : f' '' (f '' s) = s := by
  rw [Set.image_image, image_congr hf, image_id']

theorem LeftInvOn.image_image' (hf : LeftInvOn f' f s) (hs : s‚ÇÅ ‚äÜ s) : f' '' (f '' s‚ÇÅ) = s‚ÇÅ :=
  (hf.mono hs).image_image

/-! ### Right inverse -/


/-- `g` is a right inverse to `f` on `b` if `f (g x) = x` for all `x ‚àà b`. -/
@[reducible]
def RightInvOn (f' : Œ≤ ‚Üí Œ±) (f : Œ± ‚Üí Œ≤) (t : Set Œ≤) : Prop :=
  LeftInvOn f f' t

theorem RightInvOn.eqOn (h : RightInvOn f' f t) : EqOn (f ‚àò f') id t :=
  h

theorem RightInvOn.eq (h : RightInvOn f' f t) {y} (hy : y ‚àà t) : f (f' y) = y :=
  h hy

theorem LeftInvOn.rightInvOn_image (h : LeftInvOn f' f s) : RightInvOn f' f (f '' s) :=
  fun _y ‚ü®_x, hx, heq‚ü© => heq ‚ñ∏ (congr_arg f <| h.eq hx)

theorem RightInvOn.congr_left (h‚ÇÅ : RightInvOn f‚ÇÅ' f t) (heq : EqOn f‚ÇÅ' f‚ÇÇ' t) :
    RightInvOn f‚ÇÇ' f t :=
  h‚ÇÅ.congr_right heq

theorem RightInvOn.congr_right (h‚ÇÅ : RightInvOn f' f‚ÇÅ t) (hg : MapsTo f' t s) (heq : EqOn f‚ÇÅ f‚ÇÇ s) :
    RightInvOn f' f‚ÇÇ t :=
  LeftInvOn.congr_left h‚ÇÅ hg heq

theorem RightInvOn.surjOn (hf : RightInvOn f' f t) (hf' : MapsTo f' t s) : SurjOn f s t :=
  LeftInvOn.surjOn hf hf'

theorem RightInvOn.mapsTo (h : RightInvOn f' f t) (hf : SurjOn f' t s) : MapsTo f s t :=
  LeftInvOn.mapsTo h hf

lemma rightInvOn_id (s : Set Œ±) : RightInvOn id id s := fun _ _ ‚Ü¶ rfl

theorem RightInvOn.comp (hf : RightInvOn f' f t) (hg : RightInvOn g' g p) (g'pt : MapsTo g' p t) :
    RightInvOn (f' ‚àò g') (g ‚àò f) p :=
  LeftInvOn.comp hg hf g'pt

theorem RightInvOn.mono (hf : RightInvOn f' f t) (ht : t‚ÇÅ ‚äÜ t) : RightInvOn f' f t‚ÇÅ :=
  LeftInvOn.mono hf ht

theorem InjOn.rightInvOn_of_leftInvOn (hf : InjOn f s) (hf' : LeftInvOn f f' t)
    (h‚ÇÅ : MapsTo f s t) (h‚ÇÇ : MapsTo f' t s) : RightInvOn f f' s := fun _ h =>
  hf (h‚ÇÇ <| h‚ÇÅ h) h (hf' (h‚ÇÅ h))

theorem eqOn_of_leftInvOn_of_rightInvOn (h‚ÇÅ : LeftInvOn f‚ÇÅ' f s) (h‚ÇÇ : RightInvOn f‚ÇÇ' f t)
    (h : MapsTo f‚ÇÇ' t s) : EqOn f‚ÇÅ' f‚ÇÇ' t := fun y hy =>
  calc
    f‚ÇÅ' y = (f‚ÇÅ' ‚àò f ‚àò f‚ÇÇ') y := congr_arg f‚ÇÅ' (h‚ÇÇ hy).symm
    _ = f‚ÇÇ' y := h‚ÇÅ (h hy)

theorem SurjOn.leftInvOn_of_rightInvOn (hf : SurjOn f s t) (hf' : RightInvOn f f' s) :
    LeftInvOn f f' t := fun y hy => by
  let ‚ü®x, hx, heq‚ü© := hf hy
  rw [‚Üê heq, hf' hx]

/-! ### Two-side inverses -/


/-- `g` is an inverse to `f` viewed as a map from `a` to `b` -/
def InvOn (g : Œ≤ ‚Üí Œ±) (f : Œ± ‚Üí Œ≤) (s : Set Œ±) (t : Set Œ≤) : Prop :=
  LeftInvOn g f s ‚àß RightInvOn g f t

lemma invOn_id (s : Set Œ±) : InvOn id id s s := ‚ü®s.leftInvOn_id, s.rightInvOn_id‚ü©

lemma InvOn.comp (hf : InvOn f' f s t) (hg : InvOn g' g t p) (fst : MapsTo f s t)
    (g'pt : MapsTo g' p t) :
    InvOn (f' ‚àò g') (g ‚àò f) s p :=
  ‚ü®hf.1.comp hg.1 fst, hf.2.comp hg.2 g'pt‚ü©

@[symm]
theorem InvOn.symm (h : InvOn f' f s t) : InvOn f f' t s :=
  ‚ü®h.right, h.left‚ü©

theorem InvOn.mono (h : InvOn f' f s t) (hs : s‚ÇÅ ‚äÜ s) (ht : t‚ÇÅ ‚äÜ t) : InvOn f' f s‚ÇÅ t‚ÇÅ :=
  ‚ü®h.1.mono hs, h.2.mono ht‚ü©

/-- If functions `f'` and `f` are inverse on `s` and `t`, `f` maps `s` into `t`, and `f'` maps `t`
into `s`, then `f` is a bijection between `s` and `t`. The `mapsTo` arguments can be deduced from
`surjOn` statements using `LeftInvOn.mapsTo` and `RightInvOn.mapsTo`. -/
theorem InvOn.bijOn (h : InvOn f' f s t) (hf : MapsTo f s t) (hf' : MapsTo f' t s) : BijOn f s t :=
  ‚ü®hf, h.left.injOn, h.right.surjOn hf'‚ü©

end Set

/-! ### `invFunOn` is a left/right inverse -/


namespace Function

variable [Nonempty Œ±] {s : Set Œ±} {f : Œ± ‚Üí Œ≤} {a : Œ±} {b : Œ≤}

attribute [local instance] Classical.propDecidable

/-- Construct the inverse for a function `f` on domain `s`. This function is a right inverse of `f`
on `f '' s`. For a computable version, see `Function.Injective.inv_of_mem_range`. -/
noncomputable def invFunOn (f : Œ± ‚Üí Œ≤) (s : Set Œ±) (b : Œ≤) : Œ± :=
  if h : ‚àÉ a, a ‚àà s ‚àß f a = b then Classical.choose h else Classical.choice ‚ÄπNonempty Œ±‚Ä∫

theorem invFunOn_pos (h : ‚àÉ a ‚àà s, f a = b) : invFunOn f s b ‚àà s ‚àß f (invFunOn f s b) = b := by
  rw [invFunOn, dif_pos h]
  exact Classical.choose_spec h

theorem invFunOn_mem (h : ‚àÉ a ‚àà s, f a = b) : invFunOn f s b ‚àà s :=
  (invFunOn_pos h).left

theorem invFunOn_eq (h : ‚àÉ a ‚àà s, f a = b) : f (invFunOn f s b) = b :=
  (invFunOn_pos h).right

theorem invFunOn_neg (h : ¬¨‚àÉ a ‚àà s, f a = b) : invFunOn f s b = Classical.choice ‚ÄπNonempty Œ±‚Ä∫ :=
  by rw [invFunOn, dif_neg h]

@[simp]
theorem invFunOn_apply_mem (h : a ‚àà s) : invFunOn f s (f a) ‚àà s :=
  invFunOn_mem ‚ü®a, h, rfl‚ü©

theorem invFunOn_apply_eq (h : a ‚àà s) : f (invFunOn f s (f a)) = f a :=
  invFunOn_eq ‚ü®a, h, rfl‚ü©

end Function

open Function

namespace Set

variable {s s‚ÇÅ s‚ÇÇ : Set Œ±} {t : Set Œ≤} {f : Œ± ‚Üí Œ≤}

theorem InjOn.leftInvOn_invFunOn [Nonempty Œ±] (h : InjOn f s) : LeftInvOn (invFunOn f s) f s :=
  fun _a ha => h (invFunOn_apply_mem ha) ha (invFunOn_apply_eq ha)

theorem InjOn.invFunOn_image [Nonempty Œ±] (h : InjOn f s‚ÇÇ) (ht : s‚ÇÅ ‚äÜ s‚ÇÇ) :
    invFunOn f s‚ÇÇ '' (f '' s‚ÇÅ) = s‚ÇÅ :=
  h.leftInvOn_invFunOn.image_image' ht

theorem _root_.Function.leftInvOn_invFunOn_of_subset_image_image [Nonempty Œ±]
    (h : s ‚äÜ (invFunOn f s) '' (f '' s)) : LeftInvOn (invFunOn f s) f s :=
  fun x hx ‚Ü¶ by
    obtain ‚ü®-, ‚ü®x, hx', rfl‚ü©, rfl‚ü© := h hx
    rw [invFunOn_apply_eq (f := f) hx']

theorem injOn_iff_invFunOn_image_image_eq_self [Nonempty Œ±] :
    InjOn f s ‚Üî (invFunOn f s) '' (f '' s) = s :=
  ‚ü®fun h ‚Ü¶ h.invFunOn_image Subset.rfl, fun h ‚Ü¶
    (Function.leftInvOn_invFunOn_of_subset_image_image h.symm.subset).injOn‚ü©

theorem _root_.Function.invFunOn_injOn_image [Nonempty Œ±] (f : Œ± ‚Üí Œ≤) (s : Set Œ±) :
    Set.InjOn (invFunOn f s) (f '' s) := by
  rintro _ ‚ü®x, hx, rfl‚ü© _ ‚ü®x', hx', rfl‚ü© he
  rw [‚ÜêinvFunOn_apply_eq (f := f) hx, he, invFunOn_apply_eq (f := f) hx']

theorem _root_.Function.invFunOn_image_image_subset [Nonempty Œ±] (f : Œ± ‚Üí Œ≤) (s : Set Œ±) :
    (invFunOn f s) '' (f '' s) ‚äÜ s := by
  rintro _ ‚ü®_, ‚ü®x,hx,rfl‚ü©, rfl‚ü©; exact invFunOn_apply_mem hx

theorem SurjOn.rightInvOn_invFunOn [Nonempty Œ±] (h : SurjOn f s t) :
    RightInvOn (invFunOn f s) f t := fun _y hy => invFunOn_eq <| h hy

theorem BijOn.invOn_invFunOn [Nonempty Œ±] (h : BijOn f s t) : InvOn (invFunOn f s) f s t :=
  ‚ü®h.injOn.leftInvOn_invFunOn, h.surjOn.rightInvOn_invFunOn‚ü©

theorem SurjOn.invOn_invFunOn [Nonempty Œ±] (h : SurjOn f s t) :
    InvOn (invFunOn f s) f (invFunOn f s '' t) t := by
  refine' ‚ü®_, h.rightInvOn_invFunOn‚ü©
  rintro _ ‚ü®y, hy, rfl‚ü©
  rw [h.rightInvOn_invFunOn hy]

theorem SurjOn.mapsTo_invFunOn [Nonempty Œ±] (h : SurjOn f s t) : MapsTo (invFunOn f s) t s :=
  fun _y hy => mem_preimage.2 <| invFunOn_mem <| h hy

theorem SurjOn.bijOn_subset [Nonempty Œ±] (h : SurjOn f s t) : BijOn f (invFunOn f s '' t) t := by
  refine' h.invOn_invFunOn.bijOn _ (mapsTo_image _ _)
  rintro _ ‚ü®y, hy, rfl‚ü©
  rwa [h.rightInvOn_invFunOn hy]

theorem surjOn_iff_exists_bijOn_subset : SurjOn f s t ‚Üî ‚àÉ (s' : _) (_ : s' ‚äÜ s), BijOn f s' t := by
  constructor
  ¬∑ rcases eq_empty_or_nonempty t with (rfl | ht)
    ¬∑ exact fun _ => ‚ü®‚àÖ, empty_subset _, bijOn_empty f‚ü©
    ¬∑ intro h
      haveI : Nonempty Œ± := ‚ü®Classical.choose (h.comap_nonempty ht)‚ü©
      exact ‚ü®_, h.mapsTo_invFunOn.image_subset, h.bijOn_subset‚ü©
  ¬∑ rintro ‚ü®s', hs', hfs'‚ü©
    exact hfs'.surjOn.mono hs' (Subset.refl _)

theorem preimage_invFun_of_mem [n : Nonempty Œ±] {f : Œ± ‚Üí Œ≤} (hf : Injective f) {s : Set Œ±}
    (h : Classical.choice n ‚àà s) : invFun f ‚Åª¬π' s = f '' s ‚à™ (range f)·∂ú := by
  ext x
  rcases em (x ‚àà range f) with (‚ü®a, rfl‚ü© | hx)
  ¬∑ simp only [mem_preimage, mem_union, mem_compl_iff, mem_range_self, not_true, or_false,
      leftInverse_invFun hf _, hf.mem_set_image]
  ¬∑ simp only [mem_preimage, invFun_neg hx, h, hx, mem_union, mem_compl_iff, not_false_iff, or_true]

theorem preimage_invFun_of_not_mem [n : Nonempty Œ±] {f : Œ± ‚Üí Œ≤} (hf : Injective f) {s : Set Œ±}
    (h : Classical.choice n ‚àâ s) : invFun f ‚Åª¬π' s = f '' s := by
  ext x
  rcases em (x ‚àà range f) with (‚ü®a, rfl‚ü© | hx)
  ¬∑ rw [mem_preimage, leftInverse_invFun hf, hf.mem_set_image]
  ¬∑ have : x ‚àâ f '' s := fun h' => hx (image_subset_range _ _ h')
    simp only [mem_preimage, invFun_neg hx, h, this]

lemma BijOn.symm {g : Œ≤ ‚Üí Œ±} (h : InvOn f g t s) (hf : BijOn f s t) : BijOn g t s :=
  ‚ü®h.2.mapsTo hf.surjOn, h.1.injOn, h.2.surjOn hf.mapsTo‚ü©

lemma bijOn_comm {g : Œ≤ ‚Üí Œ±} (h : InvOn f g t s) : BijOn f s t ‚Üî BijOn g t s :=
  ‚ü®BijOn.symm h, BijOn.symm h.symm‚ü©

end Set

/-! ### Monotone -/


namespace Monotone

variable [Preorder Œ±] [Preorder Œ≤] {f : Œ± ‚Üí Œ≤}

protected theorem restrict (h : Monotone f) (s : Set Œ±) : Monotone (s.restrict f) := fun _ _ hxy =>
  h hxy

protected theorem codRestrict (h : Monotone f) {s : Set Œ≤} (hs : ‚àÄ x, f x ‚àà s) :
    Monotone (s.codRestrict f hs) :=
  h

protected theorem rangeFactorization (h : Monotone f) : Monotone (Set.rangeFactorization f) :=
  h

end Monotone

/-! ### Piecewise defined function -/


namespace Set

variable {Œ¥ : Œ± ‚Üí Sort*} (s : Set Œ±) (f g : ‚àÄ i, Œ¥ i)

@[simp]
theorem piecewise_empty [‚àÄ i : Œ±, Decidable (i ‚àà (‚àÖ : Set Œ±))] : piecewise ‚àÖ f g = g := by
  ext i
  simp [piecewise]

@[simp]
theorem piecewise_univ [‚àÄ i : Œ±, Decidable (i ‚àà (Set.univ : Set Œ±))] :
    piecewise Set.univ f g = f := by
  ext i
  simp [piecewise]

--@[simp] -- Porting note: simpNF linter complains
theorem piecewise_insert_self {j : Œ±} [‚àÄ i, Decidable (i ‚àà insert j s)] :
    (insert j s).piecewise f g j = f j := by simp [piecewise]

variable [‚àÄ j, Decidable (j ‚àà s)]

instance Compl.decidableMem (j : Œ±) : Decidable (j ‚àà s·∂ú) :=
  instDecidableNot

theorem piecewise_insert [DecidableEq Œ±] (j : Œ±) [‚àÄ i, Decidable (i ‚àà insert j s)] :
    (insert j s).piecewise f g = Function.update (s.piecewise f g) j (f j) := by
  simp only [piecewise, mem_insert_iff]
  ext i
  by_cases h : i = j
  ¬∑ rw [h]
    simp
  ¬∑ by_cases h' : i ‚àà s <;> simp [h, h']

@[simp]
theorem piecewise_eq_of_mem {i : Œ±} (hi : i ‚àà s) : s.piecewise f g i = f i :=
  if_pos hi

@[simp]
theorem piecewise_eq_of_not_mem {i : Œ±} (hi : i ‚àâ s) : s.piecewise f g i = g i :=
  if_neg hi

theorem piecewise_singleton (x : Œ±) [‚àÄ y, Decidable (y ‚àà ({x} : Set Œ±))] [DecidableEq Œ±]
    (f g : Œ± ‚Üí Œ≤) : piecewise {x} f g = Function.update g x (f x) := by
  ext y
  by_cases hy : y = x
  ¬∑ subst y
    simp
  ¬∑ simp [hy]

theorem piecewise_eqOn (f g : Œ± ‚Üí Œ≤) : EqOn (s.piecewise f g) f s := fun _ =>
  piecewise_eq_of_mem _ _ _

theorem piecewise_eqOn_compl (f g : Œ± ‚Üí Œ≤) : EqOn (s.piecewise f g) g s·∂ú := fun _ =>
  piecewise_eq_of_not_mem _ _ _

theorem piecewise_le {Œ¥ : Œ± ‚Üí Type*} [‚àÄ i, Preorder (Œ¥ i)] {s : Set Œ±} [‚àÄ j, Decidable (j ‚àà s)]
    {f‚ÇÅ f‚ÇÇ g : ‚àÄ i, Œ¥ i} (h‚ÇÅ : ‚àÄ i ‚àà s, f‚ÇÅ i ‚â§ g i) (h‚ÇÇ : ‚àÄ (i) (_ : i ‚àâ s), f‚ÇÇ i ‚â§ g i) :
    s.piecewise f‚ÇÅ f‚ÇÇ ‚â§ g := fun i => if h : i ‚àà s then by simp [*] else by simp [*]

theorem le_piecewise {Œ¥ : Œ± ‚Üí Type*} [‚àÄ i, Preorder (Œ¥ i)] {s : Set Œ±} [‚àÄ j, Decidable (j ‚àà s)]
    {f‚ÇÅ f‚ÇÇ g : ‚àÄ i, Œ¥ i} (h‚ÇÅ : ‚àÄ i ‚àà s, g i ‚â§ f‚ÇÅ i) (h‚ÇÇ : ‚àÄ (i) (_ : i ‚àâ s), g i ‚â§ f‚ÇÇ i) :
    g ‚â§ s.piecewise f‚ÇÅ f‚ÇÇ :=
  @piecewise_le Œ± (fun i => (Œ¥ i)·µí·µà) _ s _ _ _ _ h‚ÇÅ h‚ÇÇ

theorem piecewise_le_piecewise {Œ¥ : Œ± ‚Üí Type*} [‚àÄ i, Preorder (Œ¥ i)] {s : Set Œ±}
    [‚àÄ j, Decidable (j ‚àà s)] {f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ : ‚àÄ i, Œ¥ i} (h‚ÇÅ : ‚àÄ i ‚àà s, f‚ÇÅ i ‚â§ g‚ÇÅ i)
    (h‚ÇÇ : ‚àÄ (i) (_ : i ‚àâ s), f‚ÇÇ i ‚â§ g‚ÇÇ i) : s.piecewise f‚ÇÅ f‚ÇÇ ‚â§ s.piecewise g‚ÇÅ g‚ÇÇ := by
  apply piecewise_le <;> intros <;> simp [*]

@[simp]
theorem piecewise_insert_of_ne {i j : Œ±} (h : i ‚â† j) [‚àÄ i, Decidable (i ‚àà insert j s)] :
    (insert j s).piecewise f g i = s.piecewise f g i := by simp [piecewise, h]

@[simp]
theorem piecewise_compl [‚àÄ i, Decidable (i ‚àà s·∂ú)] : s·∂ú.piecewise f g = s.piecewise g f :=
  funext fun x => if hx : x ‚àà s then by simp [hx] else by simp [hx]

@[simp]
theorem piecewise_range_comp {Œπ : Sort*} (f : Œπ ‚Üí Œ±) [‚àÄ j, Decidable (j ‚àà range f)]
    (g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œ≤) : (range f).piecewise g‚ÇÅ g‚ÇÇ ‚àò f = g‚ÇÅ ‚àò f :=
  (piecewise_eqOn ..).comp_eq

theorem MapsTo.piecewise_ite {s s‚ÇÅ s‚ÇÇ : Set Œ±} {t t‚ÇÅ t‚ÇÇ : Set Œ≤} {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤}
    [‚àÄ i, Decidable (i ‚àà s)] (h‚ÇÅ : MapsTo f‚ÇÅ (s‚ÇÅ ‚à© s) (t‚ÇÅ ‚à© t))
    (h‚ÇÇ : MapsTo f‚ÇÇ (s‚ÇÇ ‚à© s·∂ú) (t‚ÇÇ ‚à© t·∂ú)) :
    MapsTo (s.piecewise f‚ÇÅ f‚ÇÇ) (s.ite s‚ÇÅ s‚ÇÇ) (t.ite t‚ÇÅ t‚ÇÇ) := by
  refine' (h‚ÇÅ.congr _).union_union (h‚ÇÇ.congr _)
  exacts [(piecewise_eqOn s f‚ÇÅ f‚ÇÇ).symm.mono (inter_subset_right _ _),
    (piecewise_eqOn_compl s f‚ÇÅ f‚ÇÇ).symm.mono (inter_subset_right _ _)]

theorem eqOn_piecewise {f f' g : Œ± ‚Üí Œ≤} {t} :
    EqOn (s.piecewise f f') g t ‚Üî EqOn f g (t ‚à© s) ‚àß EqOn f' g (t ‚à© s·∂ú) := by
  simp only [EqOn, ‚Üê forall_and]
  refine' forall_congr' fun a => _; by_cases a ‚àà s <;> simp [*]

theorem EqOn.piecewise_ite' {f f' g : Œ± ‚Üí Œ≤} {t t'} (h : EqOn f g (t ‚à© s))
    (h' : EqOn f' g (t' ‚à© s·∂ú)) : EqOn (s.piecewise f f') g (s.ite t t') := by
  simp [eqOn_piecewise, *]

theorem EqOn.piecewise_ite {f f' g : Œ± ‚Üí Œ≤} {t t'} (h : EqOn f g t) (h' : EqOn f' g t') :
    EqOn (s.piecewise f f') g (s.ite t t') :=
  (h.mono (inter_subset_left _ _)).piecewise_ite' s (h'.mono (inter_subset_left _ _))

theorem piecewise_preimage (f g : Œ± ‚Üí Œ≤) (t) : s.piecewise f g ‚Åª¬π' t = s.ite (f ‚Åª¬π' t) (g ‚Åª¬π' t) :=
  ext fun x => by by_cases x ‚àà s <;> simp [*, Set.ite]

theorem apply_piecewise {Œ¥' : Œ± ‚Üí Sort*} (h : ‚àÄ i, Œ¥ i ‚Üí Œ¥' i) {x : Œ±} :
    h x (s.piecewise f g x) = s.piecewise (fun x => h x (f x)) (fun x => h x (g x)) x := by
  by_cases hx : x ‚àà s <;> simp [hx]

theorem apply_piecewise‚ÇÇ {Œ¥' Œ¥'' : Œ± ‚Üí Sort*} (f' g' : ‚àÄ i, Œ¥' i) (h : ‚àÄ i, Œ¥ i ‚Üí Œ¥' i ‚Üí Œ¥'' i)
    {x : Œ±} :
    h x (s.piecewise f g x) (s.piecewise f' g' x) =
      s.piecewise (fun x => h x (f x) (f' x)) (fun x => h x (g x) (g' x)) x :=
  by by_cases hx : x ‚àà s <;> simp [hx]

theorem piecewise_op {Œ¥' : Œ± ‚Üí Sort*} (h : ‚àÄ i, Œ¥ i ‚Üí Œ¥' i) :
    (s.piecewise (fun x => h x (f x)) fun x => h x (g x)) = fun x => h x (s.piecewise f g x) :=
  funext fun _ => (apply_piecewise _ _ _ _).symm

theorem piecewise_op‚ÇÇ {Œ¥' Œ¥'' : Œ± ‚Üí Sort*} (f' g' : ‚àÄ i, Œ¥' i) (h : ‚àÄ i, Œ¥ i ‚Üí Œ¥' i ‚Üí Œ¥'' i) :
    (s.piecewise (fun x => h x (f x) (f' x)) fun x => h x (g x) (g' x)) = fun x =>
      h x (s.piecewise f g x) (s.piecewise f' g' x) :=
  funext fun _ => (apply_piecewise‚ÇÇ _ _ _ _ _ _).symm

@[simp]
theorem piecewise_same : s.piecewise f f = f := by
  ext x
  by_cases hx : x ‚àà s <;> simp [hx]

theorem range_piecewise (f g : Œ± ‚Üí Œ≤) : range (s.piecewise f g) = f '' s ‚à™ g '' s·∂ú := by
  ext y; constructor
  ¬∑ rintro ‚ü®x, rfl‚ü©
    by_cases h : x ‚àà s <;> [left; right] <;> use x <;> simp [h]
  ¬∑ rintro (‚ü®x, hx, rfl‚ü© | ‚ü®x, hx, rfl‚ü©) <;> use x <;> simp_all


theorem injective_piecewise_iff {f g : Œ± ‚Üí Œ≤} :
    Injective (s.piecewise f g) ‚Üî
      InjOn f s ‚àß InjOn g s·∂ú ‚àß ‚àÄ x ‚àà s, ‚àÄ (y) (_ : y ‚àâ s), f x ‚â† g y := by
  rw [injective_iff_injOn_univ, ‚Üê union_compl_self s, injOn_union (@disjoint_compl_right _ _ s),
    (piecewise_eqOn s f g).injOn_iff, (piecewise_eqOn_compl s f g).injOn_iff]
  refine' and_congr Iff.rfl (and_congr Iff.rfl <| forall‚ÇÑ_congr fun x hx y hy => _)
  rw [piecewise_eq_of_mem s f g hx, piecewise_eq_of_not_mem s f g hy]

theorem piecewise_mem_pi {Œ¥ : Œ± ‚Üí Type*} {t : Set Œ±} {t' : ‚àÄ i, Set (Œ¥ i)} {f g} (hf : f ‚àà pi t t')
    (hg : g ‚àà pi t t') : s.piecewise f g ‚àà pi t t' := by
  intro i ht
  by_cases hs : i ‚àà s <;> simp [hf i ht, hg i ht, hs]

@[simp]
theorem pi_piecewise {Œπ : Type*} {Œ± : Œπ ‚Üí Type*} (s s' : Set Œπ) (t t' : ‚àÄ i, Set (Œ± i))
    [‚àÄ x, Decidable (x ‚àà s')] : pi s (s'.piecewise t t') = pi (s ‚à© s') t ‚à© pi (s \ s') t' :=
  pi_if _ _ _

-- porting note: new lemma
theorem univ_pi_piecewise {Œπ : Type*} {Œ± : Œπ ‚Üí Type*} (s : Set Œπ) (t t' : ‚àÄ i, Set (Œ± i))
    [‚àÄ x, Decidable (x ‚àà s)] : pi univ (s.piecewise t t') = pi s t ‚à© pi s·∂ú t' := by
  simp [compl_eq_univ_diff]

theorem univ_pi_piecewise_univ {Œπ : Type*} {Œ± : Œπ ‚Üí Type*} (s : Set Œπ) (t : ‚àÄ i, Set (Œ± i))
    [‚àÄ x, Decidable (x ‚àà s)] : pi univ (s.piecewise t fun _ => univ) = pi s t := by simp

end Set

open Set

theorem StrictMonoOn.injOn [LinearOrder Œ±] [Preorder Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±}
    (H : StrictMonoOn f s) : s.InjOn f := fun x hx y hy hxy =>
  show Ordering.eq.Compares x y from (H.compares hx hy).1 hxy

theorem StrictAntiOn.injOn [LinearOrder Œ±] [Preorder Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±}
    (H : StrictAntiOn f s) : s.InjOn f :=
  @StrictMonoOn.injOn Œ± Œ≤·µí·µà _ _ f s H

theorem StrictMonoOn.comp [Preorder Œ±] [Preorder Œ≤] [Preorder Œ≥] {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} {s : Set Œ±}
    {t : Set Œ≤} (hg : StrictMonoOn g t) (hf : StrictMonoOn f s) (hs : Set.MapsTo f s t) :
    StrictMonoOn (g ‚àò f) s := fun _x hx _y hy hxy => hg (hs hx) (hs hy) <| hf hx hy hxy

theorem StrictMonoOn.comp_strictAntiOn [Preorder Œ±] [Preorder Œ≤] [Preorder Œ≥] {g : Œ≤ ‚Üí Œ≥}
    {f : Œ± ‚Üí Œ≤} {s : Set Œ±} {t : Set Œ≤} (hg : StrictMonoOn g t) (hf : StrictAntiOn f s)
    (hs : Set.MapsTo f s t) : StrictAntiOn (g ‚àò f) s := fun _x hx _y hy hxy =>
  hg (hs hy) (hs hx) <| hf hx hy hxy

theorem StrictAntiOn.comp [Preorder Œ±] [Preorder Œ≤] [Preorder Œ≥] {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} {s : Set Œ±}
    {t : Set Œ≤} (hg : StrictAntiOn g t) (hf : StrictAntiOn f s) (hs : Set.MapsTo f s t) :
    StrictMonoOn (g ‚àò f) s := fun _x hx _y hy hxy => hg (hs hy) (hs hx) <| hf hx hy hxy

theorem StrictAntiOn.comp_strictMonoOn [Preorder Œ±] [Preorder Œ≤] [Preorder Œ≥] {g : Œ≤ ‚Üí Œ≥}
    {f : Œ± ‚Üí Œ≤} {s : Set Œ±} {t : Set Œ≤} (hg : StrictAntiOn g t) (hf : StrictMonoOn f s)
    (hs : Set.MapsTo f s t) : StrictAntiOn (g ‚àò f) s := fun _x hx _y hy hxy =>
  hg (hs hx) (hs hy) <| hf hx hy hxy

@[simp]
theorem strictMono_restrict [Preorder Œ±] [Preorder Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±} :
    StrictMono (s.restrict f) ‚Üî StrictMonoOn f s := by simp [Set.restrict, StrictMono, StrictMonoOn]

alias ‚ü®_root_.StrictMono.of_restrict, _root_.StrictMonoOn.restrict‚ü© := strictMono_restrict

theorem StrictMono.codRestrict [Preorder Œ±] [Preorder Œ≤] {f : Œ± ‚Üí Œ≤} (hf : StrictMono f)
    {s : Set Œ≤} (hs : ‚àÄ x, f x ‚àà s) : StrictMono (Set.codRestrict f s hs) :=
  hf

namespace Function

open Set

variable {fa : Œ± ‚Üí Œ±} {fb : Œ≤ ‚Üí Œ≤} {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥} {s t : Set Œ±}

theorem Injective.comp_injOn (hg : Injective g) (hf : s.InjOn f) : s.InjOn (g ‚àò f) :=
  (hg.injOn univ).comp hf (mapsTo_univ _ _)

theorem Surjective.surjOn (hf : Surjective f) (s : Set Œ≤) : SurjOn f univ s :=
  (surjective_iff_surjOn_univ.1 hf).mono (Subset.refl _) (subset_univ _)

theorem LeftInverse.leftInvOn {g : Œ≤ ‚Üí Œ±} (h : LeftInverse f g) (s : Set Œ≤) : LeftInvOn f g s :=
  fun x _ => h x

theorem RightInverse.rightInvOn {g : Œ≤ ‚Üí Œ±} (h : RightInverse f g) (s : Set Œ±) :
    RightInvOn f g s := fun x _ => h x

theorem LeftInverse.rightInvOn_range {g : Œ≤ ‚Üí Œ±} (h : LeftInverse f g) :
    RightInvOn f g (range g) :=
  forall_range_iff.2 fun i => congr_arg g (h i)

namespace Semiconj

theorem mapsTo_image (h : Semiconj f fa fb) (ha : MapsTo fa s t) : MapsTo fb (f '' s) (f '' t) :=
  fun _y ‚ü®x, hx, hy‚ü© => hy ‚ñ∏ ‚ü®fa x, ha hx, h x‚ü©

theorem mapsTo_range (h : Semiconj f fa fb) : MapsTo fb (range f) (range f) := fun _y ‚ü®x, hy‚ü© =>
  hy ‚ñ∏ ‚ü®fa x, h x‚ü©

theorem surjOn_image (h : Semiconj f fa fb) (ha : SurjOn fa s t) : SurjOn fb (f '' s) (f '' t) := by
  rintro y ‚ü®x, hxt, rfl‚ü©
  rcases ha hxt with ‚ü®x, hxs, rfl‚ü©
  rw [h x]
  exact mem_image_of_mem _ (mem_image_of_mem _ hxs)

theorem surjOn_range (h : Semiconj f fa fb) (ha : Surjective fa) :
    SurjOn fb (range f) (range f) := by
  rw [‚Üê image_univ]
  exact h.surjOn_image (ha.surjOn univ)

theorem injOn_image (h : Semiconj f fa fb) (ha : InjOn fa s) (hf : InjOn f (fa '' s)) :
    InjOn fb (f '' s) := by
  rintro _ ‚ü®x, hx, rfl‚ü© _ ‚ü®y, hy, rfl‚ü© H
  simp only [‚Üê h.eq] at H
  exact congr_arg f (ha hx hy <| hf (mem_image_of_mem fa hx) (mem_image_of_mem fa hy) H)

theorem injOn_range (h : Semiconj f fa fb) (ha : Injective fa) (hf : InjOn f (range fa)) :
    InjOn fb (range f) := by
  rw [‚Üê image_univ] at *
  exact h.injOn_image (ha.injOn univ) hf

theorem bijOn_image (h : Semiconj f fa fb) (ha : BijOn fa s t) (hf : InjOn f t) :
    BijOn fb (f '' s) (f '' t) :=
  ‚ü®h.mapsTo_image ha.mapsTo, h.injOn_image ha.injOn (ha.image_eq.symm ‚ñ∏ hf),
    h.surjOn_image ha.surjOn‚ü©

theorem bijOn_range (h : Semiconj f fa fb) (ha : Bijective fa) (hf : Injective f) :
    BijOn fb (range f) (range f) := by
  rw [‚Üê image_univ]
  exact h.bijOn_image (bijective_iff_bijOn_univ.1 ha) (hf.injOn univ)

theorem mapsTo_preimage (h : Semiconj f fa fb) {s t : Set Œ≤} (hb : MapsTo fb s t) :
    MapsTo fa (f ‚Åª¬π' s) (f ‚Åª¬π' t) := fun x hx => by simp only [mem_preimage, h x, hb hx]

theorem injOn_preimage (h : Semiconj f fa fb) {s : Set Œ≤} (hb : InjOn fb s)
    (hf : InjOn f (f ‚Åª¬π' s)) : InjOn fa (f ‚Åª¬π' s) := by
  intro x hx y hy H
  have := congr_arg f H
  rw [h.eq, h.eq] at this
  exact hf hx hy (hb hx hy this)

end Semiconj

theorem update_comp_eq_of_not_mem_range' {Œ± Œ≤ : Sort _} {Œ≥ : Œ≤ ‚Üí Sort*} [DecidableEq Œ≤]
    (g : ‚àÄ b, Œ≥ b) {f : Œ± ‚Üí Œ≤} {i : Œ≤} (a : Œ≥ i) (h : i ‚àâ Set.range f) :
    (fun j => (Function.update g i a) (f j)) = fun j => g (f j) :=
  (update_comp_eq_of_forall_ne' _ _) fun x hx => h ‚ü®x, hx‚ü©

/-- Non-dependent version of `Function.update_comp_eq_of_not_mem_range'` -/
theorem update_comp_eq_of_not_mem_range {Œ± Œ≤ Œ≥ : Sort _} [DecidableEq Œ≤] (g : Œ≤ ‚Üí Œ≥) {f : Œ± ‚Üí Œ≤}
    {i : Œ≤} (a : Œ≥) (h : i ‚àâ Set.range f) : Function.update g i a ‚àò f = g ‚àò f :=
  update_comp_eq_of_not_mem_range' g a h

theorem insert_injOn (s : Set Œ±) : s·∂ú.InjOn fun a => insert a s := fun _a ha _ _ =>
  (insert_inj ha).1

theorem monotoneOn_of_rightInvOn_of_mapsTo {Œ± Œ≤ : Sort _} [PartialOrder Œ±] [LinearOrder Œ≤]
    {œÜ : Œ≤ ‚Üí Œ±} {œà : Œ± ‚Üí Œ≤} {t : Set Œ≤} {s : Set Œ±} (hœÜ : MonotoneOn œÜ t)
    (œÜœàs : Set.RightInvOn œà œÜ s) (œàts : Set.MapsTo œà s t) : MonotoneOn œà s := by
  rintro x xs y ys l
  rcases le_total (œà x) (œà y) with (œàxy|œàyx)
  ¬∑ exact œàxy
  ¬∑ have := hœÜ (œàts ys) (œàts xs) œàyx
    rw [œÜœàs.eq ys, œÜœàs.eq xs] at this
    induction le_antisymm l this
    exact le_refl _

theorem antitoneOn_of_rightInvOn_of_mapsTo {Œ± Œ≤ : Sort _} [PartialOrder Œ±] [LinearOrder Œ≤]
    {œÜ : Œ≤ ‚Üí Œ±} {œà : Œ± ‚Üí Œ≤} {t : Set Œ≤} {s : Set Œ±} (hœÜ : AntitoneOn œÜ t)
    (œÜœàs : Set.RightInvOn œà œÜ s) (œàts : Set.MapsTo œà s t) : AntitoneOn œà s :=
  (monotoneOn_of_rightInvOn_of_mapsTo hœÜ.dual_left œÜœàs œàts).dual_right

end Function

/-! ### Equivalences, permutations -/

namespace Set
variable {p : Œ≤ ‚Üí Prop} [DecidablePred p] {f : Œ± ‚âÉ Subtype p} {g g‚ÇÅ g‚ÇÇ : Perm Œ±} {s t : Set Œ±}

protected lemma MapsTo.extendDomain (h : MapsTo g s t) :
    MapsTo (g.extendDomain f) ((‚Üë) ‚àò f '' s) ((‚Üë) ‚àò f '' t) := by
  rintro _ ‚ü®a, ha, rfl‚ü©; exact ‚ü®_, h ha, by simp_rw [Function.comp_apply, extendDomain_apply_image]‚ü©

protected lemma SurjOn.extendDomain (h : SurjOn g s t) :
    SurjOn (g.extendDomain f) ((‚Üë) ‚àò f '' s) ((‚Üë) ‚àò f '' t) := by
  rintro _ ‚ü®a, ha, rfl‚ü©
  obtain ‚ü®b, hb, rfl‚ü© := h ha
  exact ‚ü®_, ‚ü®_, hb, rfl‚ü©, by simp_rw [Function.comp_apply, extendDomain_apply_image]‚ü©

protected lemma BijOn.extendDomain (h : BijOn g s t) :
    BijOn (g.extendDomain f) ((‚Üë) ‚àò f '' s) ((‚Üë) ‚àò f '' t) :=
  ‚ü®h.mapsTo.extendDomain, (g.extendDomain f).injective.injOn _, h.surjOn.extendDomain‚ü©

protected lemma LeftInvOn.extendDomain (h : LeftInvOn g‚ÇÅ g‚ÇÇ s) :
    LeftInvOn (g‚ÇÅ.extendDomain f) (g‚ÇÇ.extendDomain f) ((‚Üë) ‚àò f '' s) := by
  rintro _ ‚ü®a, ha, rfl‚ü©; simp_rw [Function.comp_apply, extendDomain_apply_image, h ha]

protected lemma RightInvOn.extendDomain (h : RightInvOn g‚ÇÅ g‚ÇÇ t) :
    RightInvOn (g‚ÇÅ.extendDomain f) (g‚ÇÇ.extendDomain f) ((‚Üë) ‚àò f '' t) := by
  rintro _ ‚ü®a, ha, rfl‚ü©; simp_rw [Function.comp_apply, extendDomain_apply_image, h ha]

protected lemma InvOn.extendDomain (h : InvOn g‚ÇÅ g‚ÇÇ s t) :
    InvOn (g‚ÇÅ.extendDomain f) (g‚ÇÇ.extendDomain f) ((‚Üë) ‚àò f '' s) ((‚Üë) ‚àò f '' t) :=
  ‚ü®h.1.extendDomain, h.2.extendDomain‚ü©

end Set

namespace Equiv
variable (e : Œ± ‚âÉ Œ≤) {s : Set Œ±} {t : Set Œ≤}

lemma bijOn' (h‚ÇÅ : MapsTo e s t) (h‚ÇÇ : MapsTo e.symm t s) : BijOn e s t :=
  ‚ü®h‚ÇÅ, e.injective.injOn _, fun b hb ‚Ü¶ ‚ü®e.symm b, h‚ÇÇ hb, apply_symm_apply _ _‚ü©‚ü©

protected lemma bijOn (h : ‚àÄ a, e a ‚àà t ‚Üî a ‚àà s) : BijOn e s t :=
  e.bijOn' (fun a ‚Ü¶ (h _).2) $ fun b hb ‚Ü¶ (h _).1 $ by rwa [apply_symm_apply]

lemma invOn : InvOn e e.symm t s :=
  ‚ü®e.rightInverse_symm.leftInvOn _, e.leftInverse_symm.leftInvOn _‚ü©

lemma bijOn_image : BijOn e s (e '' s) := (e.injective.injOn _).bijOn_image
lemma bijOn_symm_image : BijOn e.symm (e '' s) s := e.bijOn_image.symm e.invOn

variable {e}

@[simp] lemma bijOn_symm : BijOn e.symm t s ‚Üî BijOn e s t := bijOn_comm e.symm.invOn

alias ‚ü®_root_.Set.BijOn.of_equiv_symm, _root_.Set.BijOn.equiv_symm‚ü© := bijOn_symm

variable [DecidableEq Œ±] {a b : Œ±}

lemma bijOn_swap (ha : a ‚àà s) (hb : b ‚àà s) : BijOn (swap a b) s s :=
  (swap a b).bijOn $ fun x ‚Ü¶ by
    obtain rfl | hxa := eq_or_ne x a <;>
    obtain rfl | hxb := eq_or_ne x b <;>
    simp [*, swap_apply_of_ne_of_ne]

end Equiv
