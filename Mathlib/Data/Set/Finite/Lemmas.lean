/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Kyle Miller
-/
import Mathlib.Data.Finset.Max
import Mathlib.Data.Set.Finite.Basic
import Mathlib.Data.Set.Functor
import Mathlib.Data.Set.Lattice
import Mathlib.Data.Finite.Powerset
import Mathlib.Data.Finite.Prod
import Mathlib.Data.Finite.Sigma
import Mathlib.Data.Finite.Vector
import Mathlib.Logic.Embedding.Set

/-!
# Finite sets

This file provides `Fintype` instances for many set constructions. It also proves basic facts
about finite sets and gives ways to manipulate `Set.Finite` expressions.

## Implementation

A finite set is defined to be a set whose coercion to a type has a `Finite` instance.

There are two components to finiteness constructions. The first is `Fintype` instances for each
construction. This gives a way to actually compute a `Finset` that represents the set, and these
may be accessed using `set.toFinset`. This gets the `Finset` in the correct form, since otherwise
`Finset.univ : Finset s` is a `Finset` for the subtype for `s`. The second component is
"constructors" for `Set.Finite` that give proofs that `Fintype` instances exist classically given
other `Set.Finite` proofs. Unlike the `Fintype` instances, these *do not* use any decidability
instances since they do not compute anything.

## Tags

finite sets
-/

assert_not_exists OrderedRing
assert_not_exists MonoidWithZero

open Set Function

universe u v w x

variable {Î± : Type u} {Î² : Type v} {Î¹ : Sort w} {Î³ : Type x}

namespace Set

/-! ### Fintype instances

Every instance here should have a corresponding `Set.Finite` constructor in the next section.
-/

section FintypeInstances

instance fintypeiUnion [DecidableEq Î±] [Fintype (PLift Î¹)] (f : Î¹ â†’ Set Î±) [âˆ€ i, Fintype (f i)] :
    Fintype (â‹ƒ i, f i) :=
  Fintype.ofFinset (Finset.univ.biUnion fun i : PLift Î¹ => (f i.down).toFinset) <| by simp

instance fintypesUnion [DecidableEq Î±] {s : Set (Set Î±)} [Fintype s]
    [H : âˆ€ t : s, Fintype (t : Set Î±)] : Fintype (â‹ƒâ‚€ s) := by
  rw [sUnion_eq_iUnion]
  exact @Set.fintypeiUnion _ _ _ _ _ H

lemma toFinset_iUnion [Fintype Î²] [DecidableEq Î±] (f : Î² â†’ Set Î±)
    [âˆ€ w, Fintype (f w)] :
    Set.toFinset (â‹ƒ (x : Î²), f x) =
    Finset.biUnion (Finset.univ : Finset Î²) (fun x => (f x).toFinset) := by
  ext v
  simp only [mem_toFinset, mem_iUnion, Finset.mem_biUnion, Finset.mem_univ, true_and]

section monad
attribute [local instance] Set.monad

/-- If `s : Set Î±` is a set with `Fintype` instance and `f : Î± â†’ Set Î²` is a function such that
each `f a`, `a âˆˆ s`, has a `Fintype` structure, then `s >>= f` has a `Fintype` structure. -/
def fintypeBind {Î± Î²} [DecidableEq Î²] (s : Set Î±) [Fintype s] (f : Î± â†’ Set Î²)
    (H : âˆ€ a âˆˆ s, Fintype (f a)) : Fintype (s >>= f) :=
  Set.fintypeBiUnion s f H

instance fintypeBind' {Î± Î²} [DecidableEq Î²] (s : Set Î±) [Fintype s] (f : Î± â†’ Set Î²)
    [âˆ€ a, Fintype (f a)] : Fintype (s >>= f) :=
  Set.fintypeBiUnion' s f

end monad

instance fintypeRange [DecidableEq Î±] (f : Î¹ â†’ Î±) [Fintype (PLift Î¹)] : Fintype (range f) :=
  Fintype.ofFinset (Finset.univ.image <| f âˆ˜ PLift.down) <| by simp

instance fintypeProd (s : Set Î±) (t : Set Î²) [Fintype s] [Fintype t] :
    Fintype (s Ã—Ë¢ t : Set (Î± Ã— Î²)) :=
  Fintype.ofFinset (s.toFinset Ã—Ë¢ t.toFinset) <| by simp

instance fintypeOffDiag [DecidableEq Î±] (s : Set Î±) [Fintype s] : Fintype s.offDiag :=
  Fintype.ofFinset s.toFinset.offDiag <| by simp

/-- `image2 f s t` is `Fintype` if `s` and `t` are. -/
instance fintypeImage2 [DecidableEq Î³] (f : Î± â†’ Î² â†’ Î³) (s : Set Î±) (t : Set Î²) [hs : Fintype s]
    [ht : Fintype t] : Fintype (image2 f s t : Set Î³) := by
  rw [â† image_prod]
  apply Set.fintypeImage

instance fintypeSeq [DecidableEq Î²] (f : Set (Î± â†’ Î²)) (s : Set Î±) [Fintype f] [Fintype s] :
    Fintype (f.seq s) := by
  rw [seq_def]
  apply Set.fintypeBiUnion'

instance fintypeSeq' {Î± Î² : Type u} [DecidableEq Î²] (f : Set (Î± â†’ Î²)) (s : Set Î±) [Fintype f]
    [Fintype s] : Fintype (f <*> s) :=
  Set.fintypeSeq f s

end FintypeInstances

end Set

/-! ### Finite instances

There is seemingly some overlap between the following instances and the `Fintype` instances
in `Data.Set.Finite`. While every `Fintype` instance gives a `Finite` instance, those
instances that depend on `Fintype` or `Decidable` instances need an additional `Finite` instance
to be able to generally apply.

Some set instances do not appear here since they are consequences of others, for example
`Subtype.Finite` for subsets of a finite type.
-/


namespace Finite.Set

open scoped Classical

instance finite_range (f : Î¹ â†’ Î±) [Finite Î¹] : Finite (range f) := by
  haveI := Fintype.ofFinite (PLift Î¹)
  infer_instance

instance finite_iUnion [Finite Î¹] (f : Î¹ â†’ Set Î±) [âˆ€ i, Finite (f i)] : Finite (â‹ƒ i, f i) := by
  rw [iUnion_eq_range_psigma]
  apply Set.finite_range

instance finite_sUnion {s : Set (Set Î±)} [Finite s] [H : âˆ€ t : s, Finite (t : Set Î±)] :
    Finite (â‹ƒâ‚€ s) := by
  rw [sUnion_eq_iUnion]
  exact @Finite.Set.finite_iUnion _ _ _ _ H

theorem finite_biUnion {Î¹ : Type*} (s : Set Î¹) [Finite s] (t : Î¹ â†’ Set Î±)
    (H : âˆ€ i âˆˆ s, Finite (t i)) : Finite (â‹ƒ x âˆˆ s, t x) := by
  rw [biUnion_eq_iUnion]
  haveI : âˆ€ i : s, Finite (t i) := fun i => H i i.property
  infer_instance

instance finite_biUnion' {Î¹ : Type*} (s : Set Î¹) [Finite s] (t : Î¹ â†’ Set Î±) [âˆ€ i, Finite (t i)] :
    Finite (â‹ƒ x âˆˆ s, t x) :=
  finite_biUnion s t fun _ _ => inferInstance

/-- Example: `Finite (â‹ƒ (i < n), f i)` where `f : â„• â†’ Set Î±` and `[âˆ€ i, Finite (f i)]`
(when given instances from `Order.Interval.Finset.Nat`).
-/
instance finite_biUnion'' {Î¹ : Type*} (p : Î¹ â†’ Prop) [h : Finite { x | p x }] (t : Î¹ â†’ Set Î±)
    [âˆ€ i, Finite (t i)] : Finite (â‹ƒ (x) (_ : p x), t x) :=
  @Finite.Set.finite_biUnion' _ _ (setOf p) h t _

instance finite_iInter {Î¹ : Sort*} [Nonempty Î¹] (t : Î¹ â†’ Set Î±) [âˆ€ i, Finite (t i)] :
    Finite (â‹‚ i, t i) :=
  Finite.Set.subset (t <| Classical.arbitrary Î¹) (iInter_subset _ _)

instance finite_replacement [Finite Î±] (f : Î± â†’ Î²) :
    Finite {f x | x : Î±} :=
  Finite.Set.finite_range f

instance finite_prod (s : Set Î±) (t : Set Î²) [Finite s] [Finite t] :
    Finite (s Ã—Ë¢ t : Set (Î± Ã— Î²)) :=
  Finite.of_equiv _ (Equiv.Set.prod s t).symm

instance finite_image2 (f : Î± â†’ Î² â†’ Î³) (s : Set Î±) (t : Set Î²) [Finite s] [Finite t] :
    Finite (image2 f s t : Set Î³) := by
  rw [â† image_prod]
  infer_instance

instance finite_seq (f : Set (Î± â†’ Î²)) (s : Set Î±) [Finite f] [Finite s] : Finite (f.seq s) := by
  rw [seq_def]
  infer_instance

end Finite.Set

namespace Set

/-! ### Constructors for `Set.Finite`

Every constructor here should have a corresponding `Fintype` instance in the previous section
(or in the `Fintype` module).

The implementation of these constructors ideally should be no more than `Set.toFinite`,
after possibly setting up some `Fintype` and classical `Decidable` instances.
-/


section SetFiniteConstructors

theorem finite_iUnion [Finite Î¹] {f : Î¹ â†’ Set Î±} (H : âˆ€ i, (f i).Finite) : (â‹ƒ i, f i).Finite :=
  haveI := fun i => (H i).to_subtype
  toFinite _

/-- Dependent version of `Finite.biUnion`. -/
theorem Finite.biUnion' {Î¹} {s : Set Î¹} (hs : s.Finite) {t : âˆ€ i âˆˆ s, Set Î±}
    (ht : âˆ€ i (hi : i âˆˆ s), (t i hi).Finite) : (â‹ƒ i âˆˆ s, t i â€¹_â€º).Finite := by
  have := hs.to_subtype
  rw [biUnion_eq_iUnion]
  apply finite_iUnion fun i : s => ht i.1 i.2

theorem Finite.biUnion {Î¹} {s : Set Î¹} (hs : s.Finite) {t : Î¹ â†’ Set Î±}
    (ht : âˆ€ i âˆˆ s, (t i).Finite) : (â‹ƒ i âˆˆ s, t i).Finite :=
  hs.biUnion' ht

theorem Finite.sUnion {s : Set (Set Î±)} (hs : s.Finite) (H : âˆ€ t âˆˆ s, Set.Finite t) :
    (â‹ƒâ‚€ s).Finite := by
  simpa only [sUnion_eq_biUnion] using hs.biUnion H

theorem Finite.sInter {Î± : Type*} {s : Set (Set Î±)} {t : Set Î±} (ht : t âˆˆ s) (hf : t.Finite) :
    (â‹‚â‚€ s).Finite :=
  hf.subset (sInter_subset_of_mem ht)

/-- If sets `s i` are finite for all `i` from a finite set `t` and are empty for `i âˆ‰ t`, then the
union `â‹ƒ i, s i` is a finite set. -/
theorem Finite.iUnion {Î¹ : Type*} {s : Î¹ â†’ Set Î±} {t : Set Î¹} (ht : t.Finite)
    (hs : âˆ€ i âˆˆ t, (s i).Finite) (he : âˆ€ i, i âˆ‰ t â†’ s i = âˆ…) : (â‹ƒ i, s i).Finite := by
  suffices â‹ƒ i, s i âŠ† â‹ƒ i âˆˆ t, s i by exact (ht.biUnion hs).subset this
  refine iUnion_subset fun i x hx => ?_
  by_cases hi : i âˆˆ t
  Â· exact mem_biUnion hi hx
  Â· rw [he i hi, mem_empty_iff_false] at hx
    contradiction

section monad
attribute [local instance] Set.monad

theorem Finite.bind {Î± Î²} {s : Set Î±} {f : Î± â†’ Set Î²} (h : s.Finite) (hf : âˆ€ a âˆˆ s, (f a).Finite) :
    (s >>= f).Finite :=
  h.biUnion hf

end monad

theorem finite_range (f : Î¹ â†’ Î±) [Finite Î¹] : (range f).Finite :=
  toFinite _

theorem Finite.dependent_image {s : Set Î±} (hs : s.Finite) (F : âˆ€ i âˆˆ s, Î²) :
    {y : Î² | âˆƒ x hx, F x hx = y}.Finite := by
  have := hs.to_subtype
  simpa [range] using finite_range fun x : s => F x x.2

section preimage
variable {f : Î± â†’ Î²} {s : Set Î²}

theorem Finite.preimage' (h : s.Finite) (hf : âˆ€ b âˆˆ s, (f â»Â¹' {b}).Finite) :
    (f â»Â¹' s).Finite := by
  rw [â† Set.biUnion_preimage_singleton]
  exact Set.Finite.biUnion h hf

end preimage

section Prod

variable {s : Set Î±} {t : Set Î²}

protected theorem Finite.prod (hs : s.Finite) (ht : t.Finite) : (s Ã—Ë¢ t : Set (Î± Ã— Î²)).Finite := by
  have := hs.to_subtype
  have := ht.to_subtype
  apply toFinite

theorem Finite.of_prod_left (h : (s Ã—Ë¢ t : Set (Î± Ã— Î²)).Finite) : t.Nonempty â†’ s.Finite :=
  fun âŸ¨b, hbâŸ© => (h.image Prod.fst).subset fun a ha => âŸ¨(a, b), âŸ¨ha, hbâŸ©, rflâŸ©

theorem Finite.of_prod_right (h : (s Ã—Ë¢ t : Set (Î± Ã— Î²)).Finite) : s.Nonempty â†’ t.Finite :=
  fun âŸ¨a, haâŸ© => (h.image Prod.snd).subset fun b hb => âŸ¨(a, b), âŸ¨ha, hbâŸ©, rflâŸ©

protected theorem Infinite.prod_left (hs : s.Infinite) (ht : t.Nonempty) : (s Ã—Ë¢ t).Infinite :=
  fun h => hs <| h.of_prod_left ht

protected theorem Infinite.prod_right (ht : t.Infinite) (hs : s.Nonempty) : (s Ã—Ë¢ t).Infinite :=
  fun h => ht <| h.of_prod_right hs

protected theorem infinite_prod :
    (s Ã—Ë¢ t).Infinite â†” s.Infinite âˆ§ t.Nonempty âˆ¨ t.Infinite âˆ§ s.Nonempty := by
  refine âŸ¨fun h => ?_, ?_âŸ©
  Â· simp_rw [Set.Infinite, @and_comm Â¬_, â† Classical.not_imp]
    by_contra!
    exact h ((this.1 h.nonempty.snd).prod <| this.2 h.nonempty.fst)
  Â· rintro (h | h)
    Â· exact h.1.prod_left h.2
    Â· exact h.1.prod_right h.2

theorem finite_prod : (s Ã—Ë¢ t).Finite â†” (s.Finite âˆ¨ t = âˆ…) âˆ§ (t.Finite âˆ¨ s = âˆ…) := by
  simp only [â† not_infinite, Set.infinite_prod, not_or, not_and_or, not_nonempty_iff_eq_empty]

protected theorem Finite.offDiag {s : Set Î±} (hs : s.Finite) : s.offDiag.Finite :=
  (hs.prod hs).subset s.offDiag_subset_prod

protected theorem Finite.image2 (f : Î± â†’ Î² â†’ Î³) (hs : s.Finite) (ht : t.Finite) :
    (image2 f s t).Finite := by
  have := hs.to_subtype
  have := ht.to_subtype
  apply toFinite

end Prod

theorem Finite.seq {f : Set (Î± â†’ Î²)} {s : Set Î±} (hf : f.Finite) (hs : s.Finite) :
    (f.seq s).Finite :=
  hf.image2 _ hs

theorem Finite.seq' {Î± Î² : Type u} {f : Set (Î± â†’ Î²)} {s : Set Î±} (hf : f.Finite) (hs : s.Finite) :
    (f <*> s).Finite :=
  hf.seq hs

/-- There are finitely many subsets of a given finite set -/
theorem Finite.finite_subsets {Î± : Type u} {a : Set Î±} (h : a.Finite) : { b | b âŠ† a }.Finite := by
  convert ((Finset.powerset h.toFinset).map Finset.coeEmb.1).finite_toSet
  ext s
  simpa [â† @exists_finite_iff_finset Î± fun t => t âŠ† a âˆ§ t = s, Finite.subset_toFinset,
    â† and_assoc, Finset.coeEmb] using h.subset

protected theorem Finite.powerset {s : Set Î±} (h : s.Finite) : (ğ’« s).Finite :=
  h.finite_subsets

section Pi
variable {Î¹ : Type*} [Finite Î¹] {Îº : Î¹ â†’ Type*} {t : âˆ€ i, Set (Îº i)}

/-- Finite product of finite sets is finite -/
theorem Finite.pi (ht : âˆ€ i, (t i).Finite) : (pi univ t).Finite := by
  cases nonempty_fintype Î¹
  lift t to âˆ€ d, Finset (Îº d) using ht
  classical
    rw [â† Fintype.coe_piFinset]
    apply Finset.finite_toSet

/-- Finite product of finite sets is finite. Note this is a variant of `Set.Finite.pi` without the
extra `i âˆˆ univ` binder. -/
lemma Finite.pi' (ht : âˆ€ i, (t i).Finite) : {f : âˆ€ i, Îº i | âˆ€ i, f i âˆˆ t i}.Finite := by
  simpa [Set.pi] using Finite.pi ht

end Pi

/-- A finite union of finsets is finite. -/
theorem union_finset_finite_of_range_finite (f : Î± â†’ Finset Î²) (h : (range f).Finite) :
    (â‹ƒ a, (f a : Set Î²)).Finite := by
  rw [â† biUnion_range]
  exact h.biUnion fun y _ => y.finite_toSet

end SetFiniteConstructors

/-! ### Properties -/

theorem Finite.toFinset_prod {s : Set Î±} {t : Set Î²} (hs : s.Finite) (ht : t.Finite) :
    hs.toFinset Ã—Ë¢ ht.toFinset = (hs.prod ht).toFinset :=
  Finset.ext <| by simp

theorem Finite.toFinset_offDiag {s : Set Î±} [DecidableEq Î±] (hs : s.Finite) :
    hs.offDiag.toFinset = hs.toFinset.offDiag :=
  Finset.ext <| by simp

theorem Finite.fin_embedding {s : Set Î±} (h : s.Finite) :
    âˆƒ (n : â„•) (f : Fin n â†ª Î±), range f = s :=
  âŸ¨_, (Fintype.equivFin (h.toFinset : Set Î±)).symm.asEmbedding, by
    simp only [Finset.coe_sort_coe, Equiv.asEmbedding_range, Finite.coe_toFinset, setOf_mem_eq]âŸ©

theorem Finite.fin_param {s : Set Î±} (h : s.Finite) :
    âˆƒ (n : â„•) (f : Fin n â†’ Î±), Injective f âˆ§ range f = s :=
  let âŸ¨n, f, hfâŸ© := h.fin_embedding
  âŸ¨n, f, f.injective, hfâŸ©

theorem finite_image_fst_and_snd_iff {s : Set (Î± Ã— Î²)} :
    (Prod.fst '' s).Finite âˆ§ (Prod.snd '' s).Finite â†” s.Finite :=
  âŸ¨fun h => (h.1.prod h.2).subset fun _ h => âŸ¨mem_image_of_mem _ h, mem_image_of_mem _ hâŸ©,
    fun h => âŸ¨h.image _, h.image _âŸ©âŸ©

theorem forall_finite_image_eval_iff {Î´ : Type*} [Finite Î´] {Îº : Î´ â†’ Type*} {s : Set (âˆ€ d, Îº d)} :
    (âˆ€ d, (eval d '' s).Finite) â†” s.Finite :=
  âŸ¨fun h => (Finite.pi h).subset <| subset_pi_eval_image _ _, fun h _ => h.image _âŸ©

theorem finite_subset_iUnion {s : Set Î±} (hs : s.Finite) {Î¹} {t : Î¹ â†’ Set Î±} (h : s âŠ† â‹ƒ i, t i) :
    âˆƒ I : Set Î¹, I.Finite âˆ§ s âŠ† â‹ƒ i âˆˆ I, t i := by
  have := hs.to_subtype
  choose f hf using show âˆ€ x : s, âˆƒ i, x.1 âˆˆ t i by simpa [subset_def] using h
  refine âŸ¨range f, finite_range f, fun x hx => ?_âŸ©
  rw [biUnion_range, mem_iUnion]
  exact âŸ¨âŸ¨x, hxâŸ©, hf _âŸ©

theorem eq_finite_iUnion_of_finite_subset_iUnion {Î¹} {s : Î¹ â†’ Set Î±} {t : Set Î±} (tfin : t.Finite)
    (h : t âŠ† â‹ƒ i, s i) :
    âˆƒ I : Set Î¹,
      I.Finite âˆ§
        âˆƒ Ïƒ : { i | i âˆˆ I } â†’ Set Î±, (âˆ€ i, (Ïƒ i).Finite) âˆ§ (âˆ€ i, Ïƒ i âŠ† s i) âˆ§ t = â‹ƒ i, Ïƒ i :=
  let âŸ¨I, Ifin, hIâŸ© := finite_subset_iUnion tfin h
  âŸ¨I, Ifin, fun x => s x âˆ© t, fun _ => tfin.subset inter_subset_right, fun _ =>
    inter_subset_left, by
    ext x
    rw [mem_iUnion]
    constructor
    Â· intro x_in
      rcases mem_iUnion.mp (hI x_in) with âŸ¨i, _, âŸ¨hi, rflâŸ©, HâŸ©
      exact âŸ¨âŸ¨i, hiâŸ©, âŸ¨H, x_inâŸ©âŸ©
    Â· rintro âŸ¨i, -, HâŸ©
      exact HâŸ©

/-- Induction up to a finite set `S`. -/
theorem Finite.induction_to {C : Set Î± â†’ Prop} {S : Set Î±} (h : S.Finite)
    (S0 : Set Î±) (hS0 : S0 âŠ† S) (H0 : C S0) (H1 : âˆ€ s âŠ‚ S, C s â†’ âˆƒ a âˆˆ S \ s, C (insert a s)) :
    C S := by
  have : Finite S := Finite.to_subtype h
  have : Finite {T : Set Î± // T âŠ† S} := Finite.of_equiv (Set S) (Equiv.Set.powerset S).symm
  rw [â† Subtype.coe_mk (p := (Â· âŠ† S)) _ le_rfl]
  rw [â† Subtype.coe_mk (p := (Â· âŠ† S)) _ hS0] at H0
  refine Finite.to_wellFoundedGT.wf.induction_bot' (fun s hs hs' â†¦ ?_) H0
  obtain âŸ¨a, âŸ¨ha1, ha2âŸ©, ha'âŸ© := H1 s (ssubset_of_ne_of_subset hs s.2) hs'
  exact âŸ¨âŸ¨insert a s.1, insert_subset ha1 s.2âŸ©, Set.ssubset_insert ha2, ha'âŸ©

/-- Induction up to `univ`. -/
theorem Finite.induction_to_univ [Finite Î±] {C : Set Î± â†’ Prop} (S0 : Set Î±)
    (H0 : C S0) (H1 : âˆ€ S â‰  univ, C S â†’ âˆƒ a âˆ‰ S, C (insert a S)) : C univ :=
  finite_univ.induction_to S0 (subset_univ S0) H0 (by simpa [ssubset_univ_iff])

/-! ### Infinite sets -/

variable {s t : Set Î±}

section Image2

variable {f : Î± â†’ Î² â†’ Î³} {s : Set Î±} {t : Set Î²} {a : Î±} {b : Î²}

protected theorem Infinite.image2_left (hs : s.Infinite) (hb : b âˆˆ t)
    (hf : InjOn (fun a => f a b) s) : (image2 f s t).Infinite :=
  (hs.image hf).mono <| image_subset_image2_left hb

protected theorem Infinite.image2_right (ht : t.Infinite) (ha : a âˆˆ s) (hf : InjOn (f a) t) :
    (image2 f s t).Infinite :=
  (ht.image hf).mono <| image_subset_image2_right ha

theorem infinite_image2 (hfs : âˆ€ b âˆˆ t, InjOn (fun a => f a b) s) (hft : âˆ€ a âˆˆ s, InjOn (f a) t) :
    (image2 f s t).Infinite â†” s.Infinite âˆ§ t.Nonempty âˆ¨ t.Infinite âˆ§ s.Nonempty := by
  refine âŸ¨fun h => Set.infinite_prod.1 ?_, ?_âŸ©
  Â· rw [â† image_uncurry_prod] at h
    exact h.of_image _
  Â· rintro (âŸ¨hs, b, hbâŸ© | âŸ¨ht, a, haâŸ©)
    Â· exact hs.image2_left hb (hfs _ hb)
    Â· exact ht.image2_right ha (hft _ ha)

lemma finite_image2 (hfs : âˆ€ b âˆˆ t, InjOn (f Â· b) s) (hft : âˆ€ a âˆˆ s, InjOn (f a) t) :
    (image2 f s t).Finite â†” s.Finite âˆ§ t.Finite âˆ¨ s = âˆ… âˆ¨ t = âˆ… := by
  rw [â† not_infinite, infinite_image2 hfs hft]
  simp [not_or, -not_and, not_and_or, not_nonempty_iff_eq_empty]
  aesop

end Image2

theorem infinite_iUnion {Î¹ : Type*} [Infinite Î¹] {s : Î¹ â†’ Set Î±} (hs : Function.Injective s) :
    (â‹ƒ i, s i).Infinite :=
  fun hfin â†¦ @not_injective_infinite_finite Î¹ _ _ hfin.finite_subsets.to_subtype
    (fun i â†¦ âŸ¨s i, subset_iUnion _ _âŸ©) fun i j h_eq â†¦ hs (by simpa using h_eq)

theorem Infinite.biUnion {Î¹ : Type*} {s : Î¹ â†’ Set Î±} {a : Set Î¹} (ha : a.Infinite)
    (hs : a.InjOn s) : (â‹ƒ i âˆˆ a, s i).Infinite := by
  rw [biUnion_eq_iUnion]
  have _ := ha.to_subtype
  exact infinite_iUnion fun âŸ¨i,hiâŸ© âŸ¨j,hjâŸ© hij â†¦ by simp [hs hi hj hij]

theorem Infinite.sUnion {s : Set (Set Î±)} (hs : s.Infinite) : (â‹ƒâ‚€ s).Infinite := by
  rw [sUnion_eq_iUnion]
  have _ := hs.to_subtype
  exact infinite_iUnion Subtype.coe_injective

/-! ### Order properties -/

theorem exists_min_image [LinearOrder Î²] (s : Set Î±) (f : Î± â†’ Î²) (h1 : s.Finite) :
    s.Nonempty â†’ âˆƒ a âˆˆ s, âˆ€ b âˆˆ s, f a â‰¤ f b
  | âŸ¨x, hxâŸ© => by
    simpa only [exists_prop, Finite.mem_toFinset] using
      h1.toFinset.exists_min_image f âŸ¨x, h1.mem_toFinset.2 hxâŸ©

theorem exists_max_image [LinearOrder Î²] (s : Set Î±) (f : Î± â†’ Î²) (h1 : s.Finite) :
    s.Nonempty â†’ âˆƒ a âˆˆ s, âˆ€ b âˆˆ s, f b â‰¤ f a
  | âŸ¨x, hxâŸ© => by
    simpa only [exists_prop, Finite.mem_toFinset] using
      h1.toFinset.exists_max_image f âŸ¨x, h1.mem_toFinset.2 hxâŸ©

theorem exists_lower_bound_image [Nonempty Î±] [LinearOrder Î²] (s : Set Î±) (f : Î± â†’ Î²)
    (h : s.Finite) : âˆƒ a : Î±, âˆ€ b âˆˆ s, f a â‰¤ f b := by
  rcases s.eq_empty_or_nonempty with rfl | hs
  Â· exact â€¹Nonempty Î±â€º.elim fun a => âŸ¨a, fun _ => False.elimâŸ©
  Â· rcases Set.exists_min_image s f h hs with âŸ¨xâ‚€, _, hxâ‚€âŸ©
    exact âŸ¨xâ‚€, fun x hx => hxâ‚€ x hxâŸ©

theorem exists_upper_bound_image [Nonempty Î±] [LinearOrder Î²] (s : Set Î±) (f : Î± â†’ Î²)
    (h : s.Finite) : âˆƒ a : Î±, âˆ€ b âˆˆ s, f b â‰¤ f a :=
  exists_lower_bound_image (Î² := Î²áµ’áµˆ) s f h

lemma map_finite_biSup {F Î¹ : Type*} [CompleteLattice Î±] [CompleteLattice Î²] [FunLike F Î± Î²]
    [SupBotHomClass F Î± Î²] {s : Set Î¹} (hs : s.Finite) (f : F) (g : Î¹ â†’ Î±) :
    f (â¨† x âˆˆ s, g x) = â¨† x âˆˆ s, f (g x) := by
  have := map_finset_sup f hs.toFinset g
  simp only [Finset.sup_eq_iSup, hs.mem_toFinset, comp_apply] at this
  exact this

lemma map_finite_biInf {F Î¹ : Type*} [CompleteLattice Î±] [CompleteLattice Î²] [FunLike F Î± Î²]
    [InfTopHomClass F Î± Î²] {s : Set Î¹} (hs : s.Finite) (f : F) (g : Î¹ â†’ Î±) :
    f (â¨… x âˆˆ s, g x) = â¨… x âˆˆ s, f (g x) := by
  have := map_finset_inf f hs.toFinset g
  simp only [Finset.inf_eq_iInf, hs.mem_toFinset, comp_apply] at this
  exact this

lemma map_finite_iSup {F Î¹ : Type*} [CompleteLattice Î±] [CompleteLattice Î²] [FunLike F Î± Î²]
    [SupBotHomClass F Î± Î²] [Finite Î¹] (f : F) (g : Î¹ â†’ Î±) :
    f (â¨† i, g i) = â¨† i, f (g i) := by
  rw [â† iSup_univ (f := g), â† iSup_univ (f := fun i â†¦ f (g i))]
  exact map_finite_biSup finite_univ f g

lemma map_finite_iInf {F Î¹ : Type*} [CompleteLattice Î±] [CompleteLattice Î²] [FunLike F Î± Î²]
    [InfTopHomClass F Î± Î²] [Finite Î¹] (f : F) (g : Î¹ â†’ Î±) :
    f (â¨… i, g i) = â¨… i, f (g i) := by
  rw [â† iInf_univ (f := g), â† iInf_univ (f := fun i â†¦ f (g i))]
  exact map_finite_biInf finite_univ f g

theorem Finite.iSup_biInf_of_monotone {Î¹ Î¹' Î± : Type*} [Preorder Î¹'] [Nonempty Î¹']
    [IsDirected Î¹' (Â· â‰¤ Â·)] [Order.Frame Î±] {s : Set Î¹} (hs : s.Finite) {f : Î¹ â†’ Î¹' â†’ Î±}
    (hf : âˆ€ i âˆˆ s, Monotone (f i)) : â¨† j, â¨… i âˆˆ s, f i j = â¨… i âˆˆ s, â¨† j, f i j := by
  induction s, hs using Set.Finite.dinduction_on with
  | H0 => simp [iSup_const]
  | H1 _ _ ihs =>
    rw [forall_mem_insert] at hf
    simp only [iInf_insert, â† ihs hf.2]
    exact iSup_inf_of_monotone hf.1 fun jâ‚ jâ‚‚ hj => iInfâ‚‚_mono fun i hi => hf.2 i hi hj

theorem Finite.iSup_biInf_of_antitone {Î¹ Î¹' Î± : Type*} [Preorder Î¹'] [Nonempty Î¹']
    [IsDirected Î¹' (swap (Â· â‰¤ Â·))] [Order.Frame Î±] {s : Set Î¹} (hs : s.Finite) {f : Î¹ â†’ Î¹' â†’ Î±}
    (hf : âˆ€ i âˆˆ s, Antitone (f i)) : â¨† j, â¨… i âˆˆ s, f i j = â¨… i âˆˆ s, â¨† j, f i j :=
  @Finite.iSup_biInf_of_monotone Î¹ Î¹'áµ’áµˆ Î± _ _ _ _ _ hs _ fun i hi => (hf i hi).dual_left

theorem Finite.iInf_biSup_of_monotone {Î¹ Î¹' Î± : Type*} [Preorder Î¹'] [Nonempty Î¹']
    [IsDirected Î¹' (swap (Â· â‰¤ Â·))] [Order.Coframe Î±] {s : Set Î¹} (hs : s.Finite) {f : Î¹ â†’ Î¹' â†’ Î±}
    (hf : âˆ€ i âˆˆ s, Monotone (f i)) : â¨… j, â¨† i âˆˆ s, f i j = â¨† i âˆˆ s, â¨… j, f i j :=
  hs.iSup_biInf_of_antitone (Î± := Î±áµ’áµˆ) fun i hi => (hf i hi).dual_right

theorem Finite.iInf_biSup_of_antitone {Î¹ Î¹' Î± : Type*} [Preorder Î¹'] [Nonempty Î¹']
    [IsDirected Î¹' (Â· â‰¤ Â·)] [Order.Coframe Î±] {s : Set Î¹} (hs : s.Finite) {f : Î¹ â†’ Î¹' â†’ Î±}
    (hf : âˆ€ i âˆˆ s, Antitone (f i)) : â¨… j, â¨† i âˆˆ s, f i j = â¨† i âˆˆ s, â¨… j, f i j :=
  hs.iSup_biInf_of_monotone (Î± := Î±áµ’áµˆ) fun i hi => (hf i hi).dual_right

theorem iSup_iInf_of_monotone {Î¹ Î¹' Î± : Type*} [Finite Î¹] [Preorder Î¹'] [Nonempty Î¹']
    [IsDirected Î¹' (Â· â‰¤ Â·)] [Order.Frame Î±] {f : Î¹ â†’ Î¹' â†’ Î±} (hf : âˆ€ i, Monotone (f i)) :
    â¨† j, â¨… i, f i j = â¨… i, â¨† j, f i j := by
  simpa only [iInf_univ] using finite_univ.iSup_biInf_of_monotone fun i _ => hf i

theorem iSup_iInf_of_antitone {Î¹ Î¹' Î± : Type*} [Finite Î¹] [Preorder Î¹'] [Nonempty Î¹']
    [IsDirected Î¹' (swap (Â· â‰¤ Â·))] [Order.Frame Î±] {f : Î¹ â†’ Î¹' â†’ Î±} (hf : âˆ€ i, Antitone (f i)) :
    â¨† j, â¨… i, f i j = â¨… i, â¨† j, f i j :=
  @iSup_iInf_of_monotone Î¹ Î¹'áµ’áµˆ Î± _ _ _ _ _ _ fun i => (hf i).dual_left

theorem iInf_iSup_of_monotone {Î¹ Î¹' Î± : Type*} [Finite Î¹] [Preorder Î¹'] [Nonempty Î¹']
    [IsDirected Î¹' (swap (Â· â‰¤ Â·))] [Order.Coframe Î±] {f : Î¹ â†’ Î¹' â†’ Î±} (hf : âˆ€ i, Monotone (f i)) :
    â¨… j, â¨† i, f i j = â¨† i, â¨… j, f i j :=
  iSup_iInf_of_antitone (Î± := Î±áµ’áµˆ) fun i => (hf i).dual_right

theorem iInf_iSup_of_antitone {Î¹ Î¹' Î± : Type*} [Finite Î¹] [Preorder Î¹'] [Nonempty Î¹']
    [IsDirected Î¹' (Â· â‰¤ Â·)] [Order.Coframe Î±] {f : Î¹ â†’ Î¹' â†’ Î±} (hf : âˆ€ i, Antitone (f i)) :
    â¨… j, â¨† i, f i j = â¨† i, â¨… j, f i j :=
  iSup_iInf_of_monotone (Î± := Î±áµ’áµˆ) fun i => (hf i).dual_right

/-- An increasing union distributes over finite intersection. -/
theorem iUnion_iInter_of_monotone {Î¹ Î¹' Î± : Type*} [Finite Î¹] [Preorder Î¹'] [IsDirected Î¹' (Â· â‰¤ Â·)]
    [Nonempty Î¹'] {s : Î¹ â†’ Î¹' â†’ Set Î±} (hs : âˆ€ i, Monotone (s i)) :
    â‹ƒ j : Î¹', â‹‚ i : Î¹, s i j = â‹‚ i : Î¹, â‹ƒ j : Î¹', s i j :=
  iSup_iInf_of_monotone hs

/-- A decreasing union distributes over finite intersection. -/
theorem iUnion_iInter_of_antitone {Î¹ Î¹' Î± : Type*} [Finite Î¹] [Preorder Î¹']
    [IsDirected Î¹' (swap (Â· â‰¤ Â·))] [Nonempty Î¹'] {s : Î¹ â†’ Î¹' â†’ Set Î±} (hs : âˆ€ i, Antitone (s i)) :
    â‹ƒ j : Î¹', â‹‚ i : Î¹, s i j = â‹‚ i : Î¹, â‹ƒ j : Î¹', s i j :=
  iSup_iInf_of_antitone hs

/-- An increasing intersection distributes over finite union. -/
theorem iInter_iUnion_of_monotone {Î¹ Î¹' Î± : Type*} [Finite Î¹] [Preorder Î¹']
    [IsDirected Î¹' (swap (Â· â‰¤ Â·))] [Nonempty Î¹'] {s : Î¹ â†’ Î¹' â†’ Set Î±} (hs : âˆ€ i, Monotone (s i)) :
    â‹‚ j : Î¹', â‹ƒ i : Î¹, s i j = â‹ƒ i : Î¹, â‹‚ j : Î¹', s i j :=
  iInf_iSup_of_monotone hs

/-- A decreasing intersection distributes over finite union. -/
theorem iInter_iUnion_of_antitone {Î¹ Î¹' Î± : Type*} [Finite Î¹] [Preorder Î¹'] [IsDirected Î¹' (Â· â‰¤ Â·)]
    [Nonempty Î¹'] {s : Î¹ â†’ Î¹' â†’ Set Î±} (hs : âˆ€ i, Antitone (s i)) :
    â‹‚ j : Î¹', â‹ƒ i : Î¹, s i j = â‹ƒ i : Î¹, â‹‚ j : Î¹', s i j :=
  iInf_iSup_of_antitone hs

theorem iUnion_pi_of_monotone {Î¹ Î¹' : Type*} [LinearOrder Î¹'] [Nonempty Î¹'] {Î± : Î¹ â†’ Type*}
    {I : Set Î¹} {s : âˆ€ i, Î¹' â†’ Set (Î± i)} (hI : I.Finite) (hs : âˆ€ i âˆˆ I, Monotone (s i)) :
    â‹ƒ j : Î¹', I.pi (fun i => s i j) = I.pi fun i => â‹ƒ j, s i j := by
  simp only [pi_def, biInter_eq_iInter, preimage_iUnion]
  haveI := hI.fintype.finite
  refine iUnion_iInter_of_monotone (Î¹' := Î¹') (fun (i : I) jâ‚ jâ‚‚ h => ?_)
  exact preimage_mono <| hs i i.2 h

theorem iUnion_univ_pi_of_monotone {Î¹ Î¹' : Type*} [LinearOrder Î¹'] [Nonempty Î¹'] [Finite Î¹]
    {Î± : Î¹ â†’ Type*} {s : âˆ€ i, Î¹' â†’ Set (Î± i)} (hs : âˆ€ i, Monotone (s i)) :
    â‹ƒ j : Î¹', pi univ (fun i => s i j) = pi univ fun i => â‹ƒ j, s i j :=
  iUnion_pi_of_monotone finite_univ fun i _ => hs i

section

variable [Preorder Î±] [IsDirected Î± (Â· â‰¤ Â·)] [Nonempty Î±] {s : Set Î±}

/-- A finite set is bounded above. -/
protected theorem Finite.bddAbove (hs : s.Finite) : BddAbove s :=
  Finite.induction_on hs bddAbove_empty fun _ _ h => h.insert _

/-- A finite union of sets which are all bounded above is still bounded above. -/
theorem Finite.bddAbove_biUnion {I : Set Î²} {S : Î² â†’ Set Î±} (H : I.Finite) :
    BddAbove (â‹ƒ i âˆˆ I, S i) â†” âˆ€ i âˆˆ I, BddAbove (S i) :=
  Finite.induction_on H (by simp only [biUnion_empty, bddAbove_empty, forall_mem_empty])
    fun _ _ hs => by simp only [biUnion_insert, forall_mem_insert, bddAbove_union, hs]

theorem infinite_of_not_bddAbove : Â¬BddAbove s â†’ s.Infinite :=
  mt Finite.bddAbove

end

section

variable [Preorder Î±] [IsDirected Î± (Â· â‰¥ Â·)] [Nonempty Î±] {s : Set Î±}

/-- A finite set is bounded below. -/
protected theorem Finite.bddBelow (hs : s.Finite) : BddBelow s :=
  Finite.bddAbove (Î± := Î±áµ’áµˆ) hs

/-- A finite union of sets which are all bounded below is still bounded below. -/
theorem Finite.bddBelow_biUnion {I : Set Î²} {S : Î² â†’ Set Î±} (H : I.Finite) :
    BddBelow (â‹ƒ i âˆˆ I, S i) â†” âˆ€ i âˆˆ I, BddBelow (S i) :=
  Finite.bddAbove_biUnion (Î± := Î±áµ’áµˆ) H

theorem infinite_of_not_bddBelow : Â¬BddBelow s â†’ s.Infinite := mt Finite.bddBelow

end

end Set

namespace Finset

/-- A finset is bounded above. -/
protected theorem bddAbove [SemilatticeSup Î±] [Nonempty Î±] (s : Finset Î±) : BddAbove (â†‘s : Set Î±) :=
  s.finite_toSet.bddAbove

/-- A finset is bounded below. -/
protected theorem bddBelow [SemilatticeInf Î±] [Nonempty Î±] (s : Finset Î±) : BddBelow (â†‘s : Set Î±) :=
  s.finite_toSet.bddBelow

end Finset

section LinearOrder
variable [LinearOrder Î±] {s : Set Î±}

lemma Set.finite_diff_iUnion_Ioo (s : Set Î±) : (s \ â‹ƒ (x âˆˆ s) (y âˆˆ s), Ioo x y).Finite :=
  Set.finite_of_forall_not_lt_lt fun _x hx _y hy _z hz hxy hyz => hy.2 <| mem_iUnionâ‚‚_of_mem hx.1 <|
    mem_iUnionâ‚‚_of_mem hz.1 âŸ¨hxy, hyzâŸ©

lemma Set.finite_diff_iUnion_Ioo' (s : Set Î±) : (s \ â‹ƒ x : s Ã— s, Ioo x.1 x.2).Finite := by
  simpa only [iUnion, iSup_prod, iSup_subtype] using s.finite_diff_iUnion_Ioo

theorem DirectedOn.exists_mem_subset_of_finset_subset_biUnion {Î± Î¹ : Type*} {f : Î¹ â†’ Set Î±}
    {c : Set Î¹} (hn : c.Nonempty) (hc : DirectedOn (fun i j => f i âŠ† f j) c) {s : Finset Î±}
    (hs : (s : Set Î±) âŠ† â‹ƒ i âˆˆ c, f i) : âˆƒ i âˆˆ c, (s : Set Î±) âŠ† f i := by
  rw [Set.biUnion_eq_iUnion] at hs
  haveI := hn.coe_sort
  simpa using (directed_comp.2 hc.directed_val).exists_mem_subset_of_finset_subset_biUnion hs

end LinearOrder

namespace List
variable (Î±) [Finite Î±] (n : â„•)

lemma finite_length_eq : {l : List Î± | l.length = n}.Finite := Mathlib.Vector.finite

lemma finite_length_lt : {l : List Î± | l.length < n}.Finite := by
  convert (Finset.range n).finite_toSet.biUnion fun i _ â†¦ finite_length_eq Î± i; ext; simp

lemma finite_length_le : {l : List Î± | l.length â‰¤ n}.Finite := by
  simpa [Nat.lt_succ_iff] using finite_length_lt Î± (n + 1)

end List
