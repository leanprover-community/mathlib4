/-
Copyright (c) 2014 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Leonardo de Moura
-/
import Mathlib.Data.Set.Subsingleton
import Mathlib.Order.WithBot

#align_import data.set.image from "leanprover-community/mathlib"@"001ffdc42920050657fd45bd2b8bfbec8eaaeb29"

/-!
# Images and preimages of sets

## Main definitions

* `preimage f t : Set Î±` : the preimage fâ»Â¹(t) (written `f â»Â¹' t` in Lean) of a subset of Î².

* `range f : Set Î²` : the image of `univ` under `f`.
  Also works for `{p : Prop} (f : p â†’ Î±)` (unlike `image`)

## Notation

* `f â»Â¹' t` for `Set.preimage f t`

* `f '' s` for `Set.image f s`

## Tags

set, sets, image, preimage, pre-image, range

-/

universe u v

open Function Set

namespace Set

variable {Î± Î² Î³ : Type*} {Î¹ Î¹' : Sort*}

/-! ### Inverse image -/


section Preimage

variable {f : Î± â†’ Î²} {g : Î² â†’ Î³}

@[simp]
theorem preimage_empty : f â»Â¹' âˆ… = âˆ… :=
  rfl
#align set.preimage_empty Set.preimage_empty

theorem preimage_congr {f g : Î± â†’ Î²} {s : Set Î²} (h : âˆ€ x : Î±, f x = g x) : f â»Â¹' s = g â»Â¹' s := by
  congr with x
  simp [h]
#align set.preimage_congr Set.preimage_congr

@[gcongr]
theorem preimage_mono {s t : Set Î²} (h : s âŠ† t) : f â»Â¹' s âŠ† f â»Â¹' t := fun _ hx => h hx
#align set.preimage_mono Set.preimage_mono

@[simp, mfld_simps]
theorem preimage_univ : f â»Â¹' univ = univ :=
  rfl
#align set.preimage_univ Set.preimage_univ

theorem subset_preimage_univ {s : Set Î±} : s âŠ† f â»Â¹' univ :=
  subset_univ _
#align set.subset_preimage_univ Set.subset_preimage_univ

@[simp, mfld_simps]
theorem preimage_inter {s t : Set Î²} : f â»Â¹' (s âˆ© t) = f â»Â¹' s âˆ© f â»Â¹' t :=
  rfl
#align set.preimage_inter Set.preimage_inter

@[simp]
theorem preimage_union {s t : Set Î²} : f â»Â¹' (s âˆª t) = f â»Â¹' s âˆª f â»Â¹' t :=
  rfl
#align set.preimage_union Set.preimage_union

@[simp]
theorem preimage_compl {s : Set Î²} : f â»Â¹' sá¶œ = (f â»Â¹' s)á¶œ :=
  rfl
#align set.preimage_compl Set.preimage_compl

@[simp]
theorem preimage_diff (f : Î± â†’ Î²) (s t : Set Î²) : f â»Â¹' (s \ t) = f â»Â¹' s \ f â»Â¹' t :=
  rfl
#align set.preimage_diff Set.preimage_diff

open scoped symmDiff in
@[simp]
lemma preimage_symmDiff {f : Î± â†’ Î²} (s t : Set Î²) : f â»Â¹' (s âˆ† t) = (f â»Â¹' s) âˆ† (f â»Â¹' t) :=
  rfl
#align set.preimage_symm_diff Set.preimage_symmDiff

@[simp]
theorem preimage_ite (f : Î± â†’ Î²) (s tâ‚ tâ‚‚ : Set Î²) :
    f â»Â¹' s.ite tâ‚ tâ‚‚ = (f â»Â¹' s).ite (f â»Â¹' tâ‚) (f â»Â¹' tâ‚‚) :=
  rfl
#align set.preimage_ite Set.preimage_ite

@[simp]
theorem preimage_setOf_eq {p : Î± â†’ Prop} {f : Î² â†’ Î±} : f â»Â¹' { a | p a } = { a | p (f a) } :=
  rfl
#align set.preimage_set_of_eq Set.preimage_setOf_eq

@[simp]
theorem preimage_id_eq : preimage (id : Î± â†’ Î±) = id :=
  rfl
#align set.preimage_id_eq Set.preimage_id_eq

@[mfld_simps]
theorem preimage_id {s : Set Î±} : id â»Â¹' s = s :=
  rfl
#align set.preimage_id Set.preimage_id

@[simp, mfld_simps]
theorem preimage_id' {s : Set Î±} : (fun x => x) â»Â¹' s = s :=
  rfl
#align set.preimage_id' Set.preimage_id'

@[simp]
theorem preimage_const_of_mem {b : Î²} {s : Set Î²} (h : b âˆˆ s) : (fun _ : Î± => b) â»Â¹' s = univ :=
  eq_univ_of_forall fun _ => h
#align set.preimage_const_of_mem Set.preimage_const_of_mem

@[simp]
theorem preimage_const_of_not_mem {b : Î²} {s : Set Î²} (h : b âˆ‰ s) : (fun _ : Î± => b) â»Â¹' s = âˆ… :=
  eq_empty_of_subset_empty fun _ hx => h hx
#align set.preimage_const_of_not_mem Set.preimage_const_of_not_mem

theorem preimage_const (b : Î²) (s : Set Î²) [Decidable (b âˆˆ s)] :
    (fun _ : Î± => b) â»Â¹' s = if b âˆˆ s then univ else âˆ… := by
  split_ifs with hb
  exacts [preimage_const_of_mem hb, preimage_const_of_not_mem hb]
#align set.preimage_const Set.preimage_const

/-- If preimage of each singleton under `f : Î± â†’ Î²` is either empty or the whole type,
then `f` is a constant. -/
lemma exists_eq_const_of_preimage_singleton [Nonempty Î²] {f : Î± â†’ Î²}
    (hf : âˆ€ b : Î², f â»Â¹' {b} = âˆ… âˆ¨ f â»Â¹' {b} = univ) : âˆƒ b, f = const Î± b := by
  rcases em (âˆƒ b, f â»Â¹' {b} = univ) with âŸ¨b, hbâŸ© | hf'
  Â· exact âŸ¨b, funext fun x â†¦ eq_univ_iff_forall.1 hb xâŸ©
  Â· have : âˆ€ x b, f x â‰  b := fun x b â†¦
      eq_empty_iff_forall_not_mem.1 ((hf b).resolve_right fun h â†¦ hf' âŸ¨b, hâŸ©) x
    exact âŸ¨Classical.arbitrary Î², funext fun x â†¦ absurd rfl (this x _)âŸ©

theorem preimage_comp {s : Set Î³} : g âˆ˜ f â»Â¹' s = f â»Â¹' (g â»Â¹' s) :=
  rfl
#align set.preimage_comp Set.preimage_comp

theorem preimage_comp_eq : preimage (g âˆ˜ f) = preimage f âˆ˜ preimage g :=
  rfl
#align set.preimage_comp_eq Set.preimage_comp_eq

theorem preimage_iterate_eq {f : Î± â†’ Î±} {n : â„•} : Set.preimage f^[n] = (Set.preimage f)^[n] := by
  induction' n with n ih; Â· simp
  rw [iterate_succ, iterate_succ', preimage_comp_eq, ih]
#align set.preimage_iterate_eq Set.preimage_iterate_eq

theorem preimage_preimage {g : Î² â†’ Î³} {f : Î± â†’ Î²} {s : Set Î³} :
    f â»Â¹' (g â»Â¹' s) = (fun x => g (f x)) â»Â¹' s :=
  preimage_comp.symm
#align set.preimage_preimage Set.preimage_preimage

theorem eq_preimage_subtype_val_iff {p : Î± â†’ Prop} {s : Set (Subtype p)} {t : Set Î±} :
    s = Subtype.val â»Â¹' t â†” âˆ€ (x) (h : p x), (âŸ¨x, hâŸ© : Subtype p) âˆˆ s â†” x âˆˆ t :=
  âŸ¨fun s_eq x h => by
    rw [s_eq]
    simp, fun h => ext fun âŸ¨x, hxâŸ© => by simp [h]âŸ©
#align set.eq_preimage_subtype_val_iff Set.eq_preimage_subtype_val_iff

theorem nonempty_of_nonempty_preimage {s : Set Î²} {f : Î± â†’ Î²} (hf : (f â»Â¹' s).Nonempty) :
    s.Nonempty :=
  let âŸ¨x, hxâŸ© := hf
  âŸ¨f x, hxâŸ©
#align set.nonempty_of_nonempty_preimage Set.nonempty_of_nonempty_preimage

@[simp] theorem preimage_singleton_true (p : Î± â†’ Prop) : p â»Â¹' {True} = {a | p a} := by ext; simp
#align set.preimage_singleton_true Set.preimage_singleton_true

@[simp] theorem preimage_singleton_false (p : Î± â†’ Prop) : p â»Â¹' {False} = {a | Â¬p a} := by ext; simp
#align set.preimage_singleton_false Set.preimage_singleton_false

theorem preimage_subtype_coe_eq_compl {s u v : Set Î±} (hsuv : s âŠ† u âˆª v)
    (H : s âˆ© (u âˆ© v) = âˆ…) : ((â†‘) : s â†’ Î±) â»Â¹' u = ((â†‘) â»Â¹' v)á¶œ := by
  ext âŸ¨x, x_in_sâŸ©
  constructor
  Â· intro x_in_u x_in_v
    exact eq_empty_iff_forall_not_mem.mp H x âŸ¨x_in_s, âŸ¨x_in_u, x_in_vâŸ©âŸ©
  Â· intro hx
    exact Or.elim (hsuv x_in_s) id fun hx' => hx.elim hx'
#align set.preimage_subtype_coe_eq_compl Set.preimage_subtype_coe_eq_compl

end Preimage

/-! ### Image of a set under a function -/


section Image

variable {f : Î± â†’ Î²} {s t : Set Î±}

-- Porting note: `Set.image` is already defined in `Init.Set`
#align set.image Set.image

@[deprecated mem_image (since := "2024-03-23")]
theorem mem_image_iff_bex {f : Î± â†’ Î²} {s : Set Î±} {y : Î²} :
    y âˆˆ f '' s â†” âˆƒ (x : _) (_ : x âˆˆ s), f x = y :=
  bex_def.symm
#align set.mem_image_iff_bex Set.mem_image_iff_bex

theorem image_eta (f : Î± â†’ Î²) : f '' s = (fun x => f x) '' s :=
  rfl
#align set.image_eta Set.image_eta

theorem _root_.Function.Injective.mem_set_image {f : Î± â†’ Î²} (hf : Injective f) {s : Set Î±} {a : Î±} :
    f a âˆˆ f '' s â†” a âˆˆ s :=
  âŸ¨fun âŸ¨_, hb, EqâŸ© => hf Eq â–¸ hb, mem_image_of_mem fâŸ©
#align function.injective.mem_set_image Function.Injective.mem_set_image

theorem forall_mem_image {f : Î± â†’ Î²} {s : Set Î±} {p : Î² â†’ Prop} :
    (âˆ€ y âˆˆ f '' s, p y) â†” âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ p (f x) := by simp
#align set.ball_image_iff Set.forall_mem_image

theorem exists_mem_image {f : Î± â†’ Î²} {s : Set Î±} {p : Î² â†’ Prop} :
    (âˆƒ y âˆˆ f '' s, p y) â†” âˆƒ x âˆˆ s, p (f x) := by simp
#align set.bex_image_iff Set.exists_mem_image

@[deprecated (since := "2024-02-21")] alias ball_image_iff := forall_mem_image
@[deprecated (since := "2024-02-21")] alias bex_image_iff := exists_mem_image
@[deprecated (since := "2024-02-21")] alias âŸ¨_, ball_image_of_ballâŸ© := forall_mem_image

#align set.ball_image_of_ball Set.ball_image_of_ball

@[deprecated forall_mem_image (since := "2024-02-21")]
theorem mem_image_elim {f : Î± â†’ Î²} {s : Set Î±} {C : Î² â†’ Prop} (h : âˆ€ x : Î±, x âˆˆ s â†’ C (f x)) :
    âˆ€ {y : Î²}, y âˆˆ f '' s â†’ C y := forall_mem_image.2 h _
#align set.mem_image_elim Set.mem_image_elim

@[deprecated forall_mem_image (since := "2024-02-21")]
theorem mem_image_elim_on {f : Î± â†’ Î²} {s : Set Î±} {C : Î² â†’ Prop} {y : Î²} (h_y : y âˆˆ f '' s)
    (h : âˆ€ x : Î±, x âˆˆ s â†’ C (f x)) : C y := forall_mem_image.2 h _ h_y
#align set.mem_image_elim_on Set.mem_image_elim_on

-- Porting note: used to be `safe`
@[congr]
theorem image_congr {f g : Î± â†’ Î²} {s : Set Î±} (h : âˆ€ a âˆˆ s, f a = g a) : f '' s = g '' s := by
  ext x
  exact exists_congr fun a â†¦ and_congr_right fun ha â†¦ by rw [h a ha]
#align set.image_congr Set.image_congr

/-- A common special case of `image_congr` -/
theorem image_congr' {f g : Î± â†’ Î²} {s : Set Î±} (h : âˆ€ x : Î±, f x = g x) : f '' s = g '' s :=
  image_congr fun x _ => h x
#align set.image_congr' Set.image_congr'

@[gcongr]
lemma image_mono (h : s âŠ† t) : f '' s âŠ† f '' t := by
  rintro - âŸ¨a, ha, rflâŸ©; exact mem_image_of_mem f (h ha)

theorem image_comp (f : Î² â†’ Î³) (g : Î± â†’ Î²) (a : Set Î±) : f âˆ˜ g '' a = f '' (g '' a) := by aesop
#align set.image_comp Set.image_comp

theorem image_comp_eq {g : Î² â†’ Î³} : image (g âˆ˜ f) = image g âˆ˜ image f := by ext; simp

/-- A variant of `image_comp`, useful for rewriting -/
theorem image_image (g : Î² â†’ Î³) (f : Î± â†’ Î²) (s : Set Î±) : g '' (f '' s) = (fun x => g (f x)) '' s :=
  (image_comp g f s).symm
#align set.image_image Set.image_image

theorem image_comm {Î²'} {f : Î² â†’ Î³} {g : Î± â†’ Î²} {f' : Î± â†’ Î²'} {g' : Î²' â†’ Î³}
    (h_comm : âˆ€ a, f (g a) = g' (f' a)) : (s.image g).image f = (s.image f').image g' := by
  simp_rw [image_image, h_comm]
#align set.image_comm Set.image_comm

theorem _root_.Function.Semiconj.set_image {f : Î± â†’ Î²} {ga : Î± â†’ Î±} {gb : Î² â†’ Î²}
    (h : Function.Semiconj f ga gb) : Function.Semiconj (image f) (image ga) (image gb) := fun _ =>
  image_comm h
#align function.semiconj.set_image Function.Semiconj.set_image

theorem _root_.Function.Commute.set_image {f g : Î± â†’ Î±} (h : Function.Commute f g) :
    Function.Commute (image f) (image g) :=
  Function.Semiconj.set_image h
#align function.commute.set_image Function.Commute.set_image

/-- Image is monotone with respect to `âŠ†`. See `Set.monotone_image` for the statement in
terms of `â‰¤`. -/
@[gcongr]
theorem image_subset {a b : Set Î±} (f : Î± â†’ Î²) (h : a âŠ† b) : f '' a âŠ† f '' b := by
  simp only [subset_def, mem_image]
  exact fun x => fun âŸ¨w, h1, h2âŸ© => âŸ¨w, h h1, h2âŸ©
#align set.image_subset Set.image_subset

/-- `Set.image` is monotone. See `Set.image_subset` for the statement in terms of `âŠ†`. -/
lemma monotone_image {f : Î± â†’ Î²} : Monotone (image f) := fun _ _ => image_subset _
#align set.monotone_image Set.monotone_image

theorem image_union (f : Î± â†’ Î²) (s t : Set Î±) : f '' (s âˆª t) = f '' s âˆª f '' t :=
  ext fun x =>
    âŸ¨by rintro âŸ¨a, h | h, rflâŸ© <;> [left; right] <;> exact âŸ¨_, h, rflâŸ©, by
      rintro (âŸ¨a, h, rflâŸ© | âŸ¨a, h, rflâŸ©) <;> refine âŸ¨_, ?_, rflâŸ©
      Â· exact mem_union_left t h
      Â· exact mem_union_right s hâŸ©
#align set.image_union Set.image_union

@[simp]
theorem image_empty (f : Î± â†’ Î²) : f '' âˆ… = âˆ… := by
  ext
  simp
#align set.image_empty Set.image_empty

theorem image_inter_subset (f : Î± â†’ Î²) (s t : Set Î±) : f '' (s âˆ© t) âŠ† f '' s âˆ© f '' t :=
  subset_inter (image_subset _ inter_subset_left) (image_subset _ inter_subset_right)
#align set.image_inter_subset Set.image_inter_subset

theorem image_inter_on {f : Î± â†’ Î²} {s t : Set Î±} (h : âˆ€ x âˆˆ t, âˆ€ y âˆˆ s, f x = f y â†’ x = y) :
    f '' (s âˆ© t) = f '' s âˆ© f '' t :=
  (image_inter_subset _ _ _).antisymm
    fun b âŸ¨âŸ¨aâ‚, haâ‚, hâ‚âŸ©, âŸ¨aâ‚‚, haâ‚‚, hâ‚‚âŸ©âŸ© â†¦
      have : aâ‚‚ = aâ‚ := h _ haâ‚‚ _ haâ‚ (by simp [*])
      âŸ¨aâ‚, âŸ¨haâ‚, this â–¸ haâ‚‚âŸ©, hâ‚âŸ©
#align set.image_inter_on Set.image_inter_on

theorem image_inter {f : Î± â†’ Î²} {s t : Set Î±} (H : Injective f) : f '' (s âˆ© t) = f '' s âˆ© f '' t :=
  image_inter_on fun _ _ _ _ h => H h
#align set.image_inter Set.image_inter

theorem image_univ_of_surjective {Î¹ : Type*} {f : Î¹ â†’ Î²} (H : Surjective f) : f '' univ = univ :=
  eq_univ_of_forall <| by simpa [image]
#align set.image_univ_of_surjective Set.image_univ_of_surjective

@[simp]
theorem image_singleton {f : Î± â†’ Î²} {a : Î±} : f '' {a} = {f a} := by
  ext
  simp [image, eq_comm]
#align set.image_singleton Set.image_singleton

@[simp]
theorem Nonempty.image_const {s : Set Î±} (hs : s.Nonempty) (a : Î²) : (fun _ => a) '' s = {a} :=
  ext fun _ =>
    âŸ¨fun âŸ¨_, _, hâŸ© => h â–¸ mem_singleton _, fun h =>
      (eq_of_mem_singleton h).symm â–¸ hs.imp fun _ hy => âŸ¨hy, rflâŸ©âŸ©
#align set.nonempty.image_const Set.Nonempty.image_const

@[simp, mfld_simps]
theorem image_eq_empty {Î± Î²} {f : Î± â†’ Î²} {s : Set Î±} : f '' s = âˆ… â†” s = âˆ… := by
  simp only [eq_empty_iff_forall_not_mem]
  exact âŸ¨fun H a ha => H _ âŸ¨_, ha, rflâŸ©, fun H b âŸ¨_, ha, _âŸ© => H _ haâŸ©
#align set.image_eq_empty Set.image_eq_empty

-- Porting note: `compl` is already defined in `Init.Set`
theorem preimage_compl_eq_image_compl [BooleanAlgebra Î±] (S : Set Î±) :
    HasCompl.compl â»Â¹' S = HasCompl.compl '' S :=
  Set.ext fun x =>
    âŸ¨fun h => âŸ¨xá¶œ, h, compl_compl xâŸ©, fun h =>
      Exists.elim h fun _ hy => (compl_eq_comm.mp hy.2).symm.subst hy.1âŸ©
#align set.preimage_compl_eq_image_compl Set.preimage_compl_eq_image_compl

theorem mem_compl_image [BooleanAlgebra Î±] (t : Î±) (S : Set Î±) :
    t âˆˆ HasCompl.compl '' S â†” tá¶œ âˆˆ S := by
  simp [â† preimage_compl_eq_image_compl]
#align set.mem_compl_image Set.mem_compl_image

@[simp]
theorem image_id_eq : image (id : Î± â†’ Î±) = id := by ext; simp

/-- A variant of `image_id` -/
@[simp]
theorem image_id' (s : Set Î±) : (fun x => x) '' s = s := by
  ext
  simp
#align set.image_id' Set.image_id'

theorem image_id (s : Set Î±) : id '' s = s := by simp
#align set.image_id Set.image_id

lemma image_iterate_eq {f : Î± â†’ Î±} {n : â„•} : image (f^[n]) = (image f)^[n] := by
  induction n with
  | zero => simp
  | succ n ih => rw [iterate_succ', iterate_succ', â† ih, image_comp_eq]

theorem compl_compl_image [BooleanAlgebra Î±] (S : Set Î±) :
    HasCompl.compl '' (HasCompl.compl '' S) = S := by
  rw [â† image_comp, compl_comp_compl, image_id]
#align set.compl_compl_image Set.compl_compl_image

theorem image_insert_eq {f : Î± â†’ Î²} {a : Î±} {s : Set Î±} :
    f '' insert a s = insert (f a) (f '' s) := by
  ext
  simp [and_or_left, exists_or, eq_comm, or_comm, and_comm]
#align set.image_insert_eq Set.image_insert_eq

theorem image_pair (f : Î± â†’ Î²) (a b : Î±) : f '' {a, b} = {f a, f b} := by
  simp only [image_insert_eq, image_singleton]
#align set.image_pair Set.image_pair

theorem image_subset_preimage_of_inverse {f : Î± â†’ Î²} {g : Î² â†’ Î±} (I : LeftInverse g f) (s : Set Î±) :
    f '' s âŠ† g â»Â¹' s := fun _ âŸ¨a, h, eâŸ© => e â–¸ ((I a).symm â–¸ h : g (f a) âˆˆ s)
#align set.image_subset_preimage_of_inverse Set.image_subset_preimage_of_inverse

theorem preimage_subset_image_of_inverse {f : Î± â†’ Î²} {g : Î² â†’ Î±} (I : LeftInverse g f) (s : Set Î²) :
    f â»Â¹' s âŠ† g '' s := fun b h => âŸ¨f b, h, I bâŸ©
#align set.preimage_subset_image_of_inverse Set.preimage_subset_image_of_inverse

theorem image_eq_preimage_of_inverse {f : Î± â†’ Î²} {g : Î² â†’ Î±} (hâ‚ : LeftInverse g f)
    (hâ‚‚ : RightInverse g f) : image f = preimage g :=
  funext fun s =>
    Subset.antisymm (image_subset_preimage_of_inverse hâ‚ s) (preimage_subset_image_of_inverse hâ‚‚ s)
#align set.image_eq_preimage_of_inverse Set.image_eq_preimage_of_inverse

theorem mem_image_iff_of_inverse {f : Î± â†’ Î²} {g : Î² â†’ Î±} {b : Î²} {s : Set Î±} (hâ‚ : LeftInverse g f)
    (hâ‚‚ : RightInverse g f) : b âˆˆ f '' s â†” g b âˆˆ s := by
  rw [image_eq_preimage_of_inverse hâ‚ hâ‚‚]; rfl
#align set.mem_image_iff_of_inverse Set.mem_image_iff_of_inverse

theorem image_compl_subset {f : Î± â†’ Î²} {s : Set Î±} (H : Injective f) : f '' sá¶œ âŠ† (f '' s)á¶œ :=
  Disjoint.subset_compl_left <| by simp [disjoint_iff_inf_le, â† image_inter H]
#align set.image_compl_subset Set.image_compl_subset

theorem subset_image_compl {f : Î± â†’ Î²} {s : Set Î±} (H : Surjective f) : (f '' s)á¶œ âŠ† f '' sá¶œ :=
  compl_subset_iff_union.2 <| by
    rw [â† image_union]
    simp [image_univ_of_surjective H]
#align set.subset_image_compl Set.subset_image_compl

theorem image_compl_eq {f : Î± â†’ Î²} {s : Set Î±} (H : Bijective f) : f '' sá¶œ = (f '' s)á¶œ :=
  Subset.antisymm (image_compl_subset H.1) (subset_image_compl H.2)
#align set.image_compl_eq Set.image_compl_eq

theorem subset_image_diff (f : Î± â†’ Î²) (s t : Set Î±) : f '' s \ f '' t âŠ† f '' (s \ t) := by
  rw [diff_subset_iff, â† image_union, union_diff_self]
  exact image_subset f subset_union_right
#align set.subset_image_diff Set.subset_image_diff

open scoped symmDiff in
theorem subset_image_symmDiff : (f '' s) âˆ† (f '' t) âŠ† f '' s âˆ† t :=
  (union_subset_union (subset_image_diff _ _ _) <| subset_image_diff _ _ _).trans
    (superset_of_eq (image_union _ _ _))
#align set.subset_image_symm_diff Set.subset_image_symmDiff

theorem image_diff {f : Î± â†’ Î²} (hf : Injective f) (s t : Set Î±) : f '' (s \ t) = f '' s \ f '' t :=
  Subset.antisymm
    (Subset.trans (image_inter_subset _ _ _) <| inter_subset_inter_right _ <| image_compl_subset hf)
    (subset_image_diff f s t)
#align set.image_diff Set.image_diff

open scoped symmDiff in
theorem image_symmDiff (hf : Injective f) (s t : Set Î±) : f '' s âˆ† t = (f '' s) âˆ† (f '' t) := by
  simp_rw [Set.symmDiff_def, image_union, image_diff hf]
#align set.image_symm_diff Set.image_symmDiff

theorem Nonempty.image (f : Î± â†’ Î²) {s : Set Î±} : s.Nonempty â†’ (f '' s).Nonempty
  | âŸ¨x, hxâŸ© => âŸ¨f x, mem_image_of_mem f hxâŸ©
#align set.nonempty.image Set.Nonempty.image

theorem Nonempty.of_image {f : Î± â†’ Î²} {s : Set Î±} : (f '' s).Nonempty â†’ s.Nonempty
  | âŸ¨_, x, hx, _âŸ© => âŸ¨x, hxâŸ©
#align set.nonempty.of_image Set.Nonempty.of_image

@[simp]
theorem image_nonempty {f : Î± â†’ Î²} {s : Set Î±} : (f '' s).Nonempty â†” s.Nonempty :=
  âŸ¨Nonempty.of_image, fun h => h.image fâŸ©
#align set.nonempty_image_iff Set.image_nonempty

@[deprecated (since := "2024-01-06")] alias nonempty_image_iff := image_nonempty

theorem Nonempty.preimage {s : Set Î²} (hs : s.Nonempty) {f : Î± â†’ Î²} (hf : Surjective f) :
    (f â»Â¹' s).Nonempty :=
  let âŸ¨y, hyâŸ© := hs
  let âŸ¨x, hxâŸ© := hf y
  âŸ¨x, mem_preimage.2 <| hx.symm â–¸ hyâŸ©
#align set.nonempty.preimage Set.Nonempty.preimage

instance (f : Î± â†’ Î²) (s : Set Î±) [Nonempty s] : Nonempty (f '' s) :=
  (Set.Nonempty.image f nonempty_of_nonempty_subtype).to_subtype

/-- image and preimage are a Galois connection -/
@[simp]
theorem image_subset_iff {s : Set Î±} {t : Set Î²} {f : Î± â†’ Î²} : f '' s âŠ† t â†” s âŠ† f â»Â¹' t :=
  forall_mem_image
#align set.image_subset_iff Set.image_subset_iff

theorem image_preimage_subset (f : Î± â†’ Î²) (s : Set Î²) : f '' (f â»Â¹' s) âŠ† s :=
  image_subset_iff.2 Subset.rfl
#align set.image_preimage_subset Set.image_preimage_subset

theorem subset_preimage_image (f : Î± â†’ Î²) (s : Set Î±) : s âŠ† f â»Â¹' (f '' s) := fun _ =>
  mem_image_of_mem f
#align set.subset_preimage_image Set.subset_preimage_image

@[simp]
theorem preimage_image_eq {f : Î± â†’ Î²} (s : Set Î±) (h : Injective f) : f â»Â¹' (f '' s) = s :=
  Subset.antisymm (fun _ âŸ¨_, hy, eâŸ© => h e â–¸ hy) (subset_preimage_image f s)
#align set.preimage_image_eq Set.preimage_image_eq

@[simp]
theorem image_preimage_eq {f : Î± â†’ Î²} (s : Set Î²) (h : Surjective f) : f '' (f â»Â¹' s) = s :=
  Subset.antisymm (image_preimage_subset f s) fun x hx =>
    let âŸ¨y, eâŸ© := h x
    âŸ¨y, (e.symm â–¸ hx : f y âˆˆ s), eâŸ©
#align set.image_preimage_eq Set.image_preimage_eq

@[simp]
theorem Nonempty.subset_preimage_const {s : Set Î±} (hs : Set.Nonempty s) (t : Set Î²) (a : Î²) :
    s âŠ† (fun _ => a) â»Â¹' t â†” a âˆˆ t := by
  rw [â† image_subset_iff, hs.image_const, singleton_subset_iff]

@[simp]
theorem preimage_eq_preimage {f : Î² â†’ Î±} (hf : Surjective f) : f â»Â¹' s = f â»Â¹' t â†” s = t :=
  Iff.intro
    fun eq => by rw [â† image_preimage_eq s hf, â† image_preimage_eq t hf, eq]
    fun eq => eq â–¸ rfl
#align set.preimage_eq_preimage Set.preimage_eq_preimage

theorem image_inter_preimage (f : Î± â†’ Î²) (s : Set Î±) (t : Set Î²) :
    f '' (s âˆ© f â»Â¹' t) = f '' s âˆ© t := by
  apply Subset.antisymm
  Â· calc
      f '' (s âˆ© f â»Â¹' t) âŠ† f '' s âˆ© f '' (f â»Â¹' t) := image_inter_subset _ _ _
      _ âŠ† f '' s âˆ© t := inter_subset_inter_right _ (image_preimage_subset f t)
  Â· rintro _ âŸ¨âŸ¨x, h', rflâŸ©, hâŸ©
    exact âŸ¨x, âŸ¨h', hâŸ©, rflâŸ©
#align set.image_inter_preimage Set.image_inter_preimage

theorem image_preimage_inter (f : Î± â†’ Î²) (s : Set Î±) (t : Set Î²) :
    f '' (f â»Â¹' t âˆ© s) = t âˆ© f '' s := by simp only [inter_comm, image_inter_preimage]
#align set.image_preimage_inter Set.image_preimage_inter

@[simp]
theorem image_inter_nonempty_iff {f : Î± â†’ Î²} {s : Set Î±} {t : Set Î²} :
    (f '' s âˆ© t).Nonempty â†” (s âˆ© f â»Â¹' t).Nonempty := by
  rw [â† image_inter_preimage, image_nonempty]
#align set.image_inter_nonempty_iff Set.image_inter_nonempty_iff

theorem image_diff_preimage {f : Î± â†’ Î²} {s : Set Î±} {t : Set Î²} :
    f '' (s \ f â»Â¹' t) = f '' s \ t := by simp_rw [diff_eq, â† preimage_compl, image_inter_preimage]
#align set.image_diff_preimage Set.image_diff_preimage

theorem compl_image : image (compl : Set Î± â†’ Set Î±) = preimage compl :=
  image_eq_preimage_of_inverse compl_compl compl_compl
#align set.compl_image Set.compl_image

theorem compl_image_set_of {p : Set Î± â†’ Prop} : compl '' { s | p s } = { s | p sá¶œ } :=
  congr_fun compl_image p
#align set.compl_image_set_of Set.compl_image_set_of

theorem inter_preimage_subset (s : Set Î±) (t : Set Î²) (f : Î± â†’ Î²) :
    s âˆ© f â»Â¹' t âŠ† f â»Â¹' (f '' s âˆ© t) := fun _ h => âŸ¨mem_image_of_mem _ h.left, h.rightâŸ©
#align set.inter_preimage_subset Set.inter_preimage_subset

theorem union_preimage_subset (s : Set Î±) (t : Set Î²) (f : Î± â†’ Î²) :
    s âˆª f â»Â¹' t âŠ† f â»Â¹' (f '' s âˆª t) := fun _ h =>
  Or.elim h (fun l => Or.inl <| mem_image_of_mem _ l) fun r => Or.inr r
#align set.union_preimage_subset Set.union_preimage_subset

theorem subset_image_union (f : Î± â†’ Î²) (s : Set Î±) (t : Set Î²) : f '' (s âˆª f â»Â¹' t) âŠ† f '' s âˆª t :=
  image_subset_iff.2 (union_preimage_subset _ _ _)
#align set.subset_image_union Set.subset_image_union

theorem preimage_subset_iff {A : Set Î±} {B : Set Î²} {f : Î± â†’ Î²} :
    f â»Â¹' B âŠ† A â†” âˆ€ a : Î±, f a âˆˆ B â†’ a âˆˆ A :=
  Iff.rfl
#align set.preimage_subset_iff Set.preimage_subset_iff

theorem image_eq_image {f : Î± â†’ Î²} (hf : Injective f) : f '' s = f '' t â†” s = t :=
  Iff.symm <|
    (Iff.intro fun eq => eq â–¸ rfl) fun eq => by
      rw [â† preimage_image_eq s hf, â† preimage_image_eq t hf, eq]
#align set.image_eq_image Set.image_eq_image

theorem subset_image_iff {t : Set Î²} :
    t âŠ† f '' s â†” âˆƒ u, u âŠ† s âˆ§ f '' u = t := by
  refine âŸ¨fun h â†¦ âŸ¨f â»Â¹' t âˆ© s, inter_subset_right, ?_âŸ©,
    fun âŸ¨u, hu, hu'âŸ© â†¦ hu'.symm â–¸ image_mono huâŸ©
  rwa [image_preimage_inter, inter_eq_left]

theorem image_subset_image_iff {f : Î± â†’ Î²} (hf : Injective f) : f '' s âŠ† f '' t â†” s âŠ† t := by
  refine Iff.symm <| (Iff.intro (image_subset f)) fun h => ?_
  rw [â† preimage_image_eq s hf, â† preimage_image_eq t hf]
  exact preimage_mono h
#align set.image_subset_image_iff Set.image_subset_image_iff

theorem prod_quotient_preimage_eq_image [s : Setoid Î±] (g : Quotient s â†’ Î²) {h : Î± â†’ Î²}
    (Hh : h = g âˆ˜ Quotient.mk'') (r : Set (Î² Ã— Î²)) :
    { x : Quotient s Ã— Quotient s | (g x.1, g x.2) âˆˆ r } =
      (fun a : Î± Ã— Î± => (âŸ¦a.1âŸ§, âŸ¦a.2âŸ§)) '' ((fun a : Î± Ã— Î± => (h a.1, h a.2)) â»Â¹' r) :=
  Hh.symm â–¸
    Set.ext fun âŸ¨aâ‚, aâ‚‚âŸ© =>
      âŸ¨Quot.induction_onâ‚‚ aâ‚ aâ‚‚ fun aâ‚ aâ‚‚ h => âŸ¨(aâ‚, aâ‚‚), h, rflâŸ©, fun âŸ¨âŸ¨bâ‚, bâ‚‚âŸ©, hâ‚, hâ‚‚âŸ© =>
        show (g aâ‚, g aâ‚‚) âˆˆ r from
          have hâ‚ƒ : âŸ¦bâ‚âŸ§ = aâ‚ âˆ§ âŸ¦bâ‚‚âŸ§ = aâ‚‚ := Prod.ext_iff.1 hâ‚‚
          hâ‚ƒ.1 â–¸ hâ‚ƒ.2 â–¸ hâ‚âŸ©
#align set.prod_quotient_preimage_eq_image Set.prod_quotient_preimage_eq_image

theorem exists_image_iff (f : Î± â†’ Î²) (x : Set Î±) (P : Î² â†’ Prop) :
    (âˆƒ a : f '' x, P a) â†” âˆƒ a : x, P (f a) :=
  âŸ¨fun âŸ¨a, hâŸ© => âŸ¨âŸ¨_, a.prop.choose_spec.1âŸ©, a.prop.choose_spec.2.symm â–¸ hâŸ©, fun âŸ¨a, hâŸ© =>
    âŸ¨âŸ¨_, _, a.prop, rflâŸ©, hâŸ©âŸ©
#align set.exists_image_iff Set.exists_image_iff

theorem imageFactorization_eq {f : Î± â†’ Î²} {s : Set Î±} :
    Subtype.val âˆ˜ imageFactorization f s = f âˆ˜ Subtype.val :=
  funext fun _ => rfl
#align set.image_factorization_eq Set.imageFactorization_eq

theorem surjective_onto_image {f : Î± â†’ Î²} {s : Set Î±} : Surjective (imageFactorization f s) :=
  fun âŸ¨_, âŸ¨a, ha, rflâŸ©âŸ© => âŸ¨âŸ¨a, haâŸ©, rflâŸ©
#align set.surjective_onto_image Set.surjective_onto_image

/-- If the only elements outside `s` are those left fixed by `Ïƒ`, then mapping by `Ïƒ` has no effect.
-/
theorem image_perm {s : Set Î±} {Ïƒ : Equiv.Perm Î±} (hs : { a : Î± | Ïƒ a â‰  a } âŠ† s) : Ïƒ '' s = s := by
  ext i
  obtain hi | hi := eq_or_ne (Ïƒ i) i
  Â· refine âŸ¨?_, fun h => âŸ¨i, h, hiâŸ©âŸ©
    rintro âŸ¨j, hj, hâŸ©
    rwa [Ïƒ.injective (hi.trans h.symm)]
  Â· refine iff_of_true âŸ¨Ïƒ.symm i, hs fun h => hi ?_, Ïƒ.apply_symm_apply _âŸ© (hs hi)
    convert congr_arg Ïƒ h <;> exact (Ïƒ.apply_symm_apply _).symm
#align set.image_perm Set.image_perm

end Image

/-! ### Lemmas about the powerset and image. -/

/-- The powerset of `{a} âˆª s` is `ğ’« s` together with `{a} âˆª t` for each `t âˆˆ ğ’« s`. -/
theorem powerset_insert (s : Set Î±) (a : Î±) : ğ’« insert a s = ğ’« s âˆª insert a '' ğ’« s := by
  ext t
  simp_rw [mem_union, mem_image, mem_powerset_iff]
  constructor
  Â· intro h
    by_cases hs : a âˆˆ t
    Â· right
      refine âŸ¨t \ {a}, ?_, ?_âŸ©
      Â· rw [diff_singleton_subset_iff]
        assumption
      Â· rw [insert_diff_singleton, insert_eq_of_mem hs]
    Â· left
      exact (subset_insert_iff_of_not_mem hs).mp h
  Â· rintro (h | âŸ¨s', hâ‚, rflâŸ©)
    Â· exact subset_trans h (subset_insert a s)
    Â· exact insert_subset_insert hâ‚
#align set.powerset_insert Set.powerset_insert

/-! ### Lemmas about range of a function. -/


section Range

variable {f : Î¹ â†’ Î±} {s t : Set Î±}

theorem forall_mem_range {p : Î± â†’ Prop} : (âˆ€ a âˆˆ range f, p a) â†” âˆ€ i, p (f i) := by simp
#align set.forall_range_iff Set.forall_mem_range

@[deprecated (since := "2024-02-21")] alias forall_range_iff := forall_mem_range

theorem forall_subtype_range_iff {p : range f â†’ Prop} :
    (âˆ€ a : range f, p a) â†” âˆ€ i, p âŸ¨f i, mem_range_self _âŸ© :=
  âŸ¨fun H i => H _, fun H âŸ¨y, i, hiâŸ© => by
    subst hi
    apply HâŸ©
#align set.forall_subtype_range_iff Set.forall_subtype_range_iff

theorem exists_range_iff {p : Î± â†’ Prop} : (âˆƒ a âˆˆ range f, p a) â†” âˆƒ i, p (f i) := by simp
#align set.exists_range_iff Set.exists_range_iff

@[deprecated (since := "2024-03-10")]
alias exists_range_iff' := exists_range_iff
#align set.exists_range_iff' Set.exists_range_iff'

theorem exists_subtype_range_iff {p : range f â†’ Prop} :
    (âˆƒ a : range f, p a) â†” âˆƒ i, p âŸ¨f i, mem_range_self _âŸ© :=
  âŸ¨fun âŸ¨âŸ¨a, i, hiâŸ©, haâŸ© => by
    subst a
    exact âŸ¨i, haâŸ©,
   fun âŸ¨i, hiâŸ© => âŸ¨_, hiâŸ©âŸ©
#align set.exists_subtype_range_iff Set.exists_subtype_range_iff

theorem range_iff_surjective : range f = univ â†” Surjective f :=
  eq_univ_iff_forall
#align set.range_iff_surjective Set.range_iff_surjective

alias âŸ¨_, _root_.Function.Surjective.range_eqâŸ© := range_iff_surjective
#align function.surjective.range_eq Function.Surjective.range_eq

@[simp]
theorem subset_range_of_surjective {f : Î± â†’ Î²} (h : Surjective f) (s : Set Î²) :
    s âŠ† range f := Surjective.range_eq h â–¸ subset_univ s

@[simp]
theorem image_univ {f : Î± â†’ Î²} : f '' univ = range f := by
  ext
  simp [image, range]
#align set.image_univ Set.image_univ

@[simp]
theorem preimage_eq_univ_iff {f : Î± â†’ Î²} {s} : f â»Â¹' s = univ â†” range f âŠ† s := by
  rw [â† univ_subset_iff, â† image_subset_iff, image_univ]

theorem image_subset_range (f : Î± â†’ Î²) (s) : f '' s âŠ† range f := by
  rw [â† image_univ]; exact image_subset _ (subset_univ _)
#align set.image_subset_range Set.image_subset_range

theorem mem_range_of_mem_image (f : Î± â†’ Î²) (s) {x : Î²} (h : x âˆˆ f '' s) : x âˆˆ range f :=
  image_subset_range f s h
#align set.mem_range_of_mem_image Set.mem_range_of_mem_image

theorem _root_.Nat.mem_range_succ (i : â„•) : i âˆˆ range Nat.succ â†” 0 < i :=
  âŸ¨by
    rintro âŸ¨n, rflâŸ©
    exact Nat.succ_pos n, fun h => âŸ¨_, Nat.succ_pred_eq_of_pos hâŸ©âŸ©
#align nat.mem_range_succ Nat.mem_range_succ

theorem Nonempty.preimage' {s : Set Î²} (hs : s.Nonempty) {f : Î± â†’ Î²} (hf : s âŠ† range f) :
    (f â»Â¹' s).Nonempty :=
  let âŸ¨_, hyâŸ© := hs
  let âŸ¨x, hxâŸ© := hf hy
  âŸ¨x, Set.mem_preimage.2 <| hx.symm â–¸ hyâŸ©
#align set.nonempty.preimage' Set.Nonempty.preimage'

theorem range_comp (g : Î± â†’ Î²) (f : Î¹ â†’ Î±) : range (g âˆ˜ f) = g '' range f := by aesop
#align set.range_comp Set.range_comp

theorem range_subset_iff : range f âŠ† s â†” âˆ€ y, f y âˆˆ s :=
  forall_mem_range
#align set.range_subset_iff Set.range_subset_iff

theorem range_subset_range_iff_exists_comp {f : Î± â†’ Î³} {g : Î² â†’ Î³} :
    range f âŠ† range g â†” âˆƒ h : Î± â†’ Î², f = g âˆ˜ h := by
  simp only [range_subset_iff, mem_range, Classical.skolem, Function.funext_iff, (Â· âˆ˜ Â·), eq_comm]

theorem range_eq_iff (f : Î± â†’ Î²) (s : Set Î²) :
    range f = s â†” (âˆ€ a, f a âˆˆ s) âˆ§ âˆ€ b âˆˆ s, âˆƒ a, f a = b := by
  rw [â† range_subset_iff]
  exact le_antisymm_iff
#align set.range_eq_iff Set.range_eq_iff

theorem range_comp_subset_range (f : Î± â†’ Î²) (g : Î² â†’ Î³) : range (g âˆ˜ f) âŠ† range g := by
  rw [range_comp]; apply image_subset_range
#align set.range_comp_subset_range Set.range_comp_subset_range

theorem range_nonempty_iff_nonempty : (range f).Nonempty â†” Nonempty Î¹ :=
  âŸ¨fun âŸ¨_, x, _âŸ© => âŸ¨xâŸ©, fun âŸ¨xâŸ© => âŸ¨f x, mem_range_self xâŸ©âŸ©
#align set.range_nonempty_iff_nonempty Set.range_nonempty_iff_nonempty

theorem range_nonempty [h : Nonempty Î¹] (f : Î¹ â†’ Î±) : (range f).Nonempty :=
  range_nonempty_iff_nonempty.2 h
#align set.range_nonempty Set.range_nonempty

@[simp]
theorem range_eq_empty_iff {f : Î¹ â†’ Î±} : range f = âˆ… â†” IsEmpty Î¹ := by
  rw [â† not_nonempty_iff, â† range_nonempty_iff_nonempty, not_nonempty_iff_eq_empty]
#align set.range_eq_empty_iff Set.range_eq_empty_iff

theorem range_eq_empty [IsEmpty Î¹] (f : Î¹ â†’ Î±) : range f = âˆ… :=
  range_eq_empty_iff.2 â€¹_â€º
#align set.range_eq_empty Set.range_eq_empty

instance instNonemptyRange [Nonempty Î¹] (f : Î¹ â†’ Î±) : Nonempty (range f) :=
  (range_nonempty f).to_subtype

@[simp]
theorem image_union_image_compl_eq_range (f : Î± â†’ Î²) : f '' s âˆª f '' sá¶œ = range f := by
  rw [â† image_union, â† image_univ, â† union_compl_self]
#align set.image_union_image_compl_eq_range Set.image_union_image_compl_eq_range

theorem insert_image_compl_eq_range (f : Î± â†’ Î²) (x : Î±) : insert (f x) (f '' {x}á¶œ) = range f := by
  rw [â† image_insert_eq, insert_eq, union_compl_self, image_univ]
#align set.insert_image_compl_eq_range Set.insert_image_compl_eq_range

theorem image_preimage_eq_range_inter {f : Î± â†’ Î²} {t : Set Î²} : f '' (f â»Â¹' t) = range f âˆ© t :=
  ext fun x =>
    âŸ¨fun âŸ¨x, hx, HEqâŸ© => HEq â–¸ âŸ¨mem_range_self _, hxâŸ©, fun âŸ¨âŸ¨y, h_eqâŸ©, hxâŸ© =>
      h_eq â–¸ mem_image_of_mem f <| show y âˆˆ f â»Â¹' t by rw [preimage, mem_setOf, h_eq]; exact hxâŸ©

theorem image_preimage_eq_inter_range {f : Î± â†’ Î²} {t : Set Î²} : f '' (f â»Â¹' t) = t âˆ© range f := by
  rw [image_preimage_eq_range_inter, inter_comm]
#align set.image_preimage_eq_inter_range Set.image_preimage_eq_inter_range

theorem image_preimage_eq_of_subset {f : Î± â†’ Î²} {s : Set Î²} (hs : s âŠ† range f) :
    f '' (f â»Â¹' s) = s := by rw [image_preimage_eq_range_inter, inter_eq_self_of_subset_right hs]
#align set.image_preimage_eq_of_subset Set.image_preimage_eq_of_subset

theorem image_preimage_eq_iff {f : Î± â†’ Î²} {s : Set Î²} : f '' (f â»Â¹' s) = s â†” s âŠ† range f :=
  âŸ¨by
    intro h
    rw [â† h]
    apply image_subset_range,
   image_preimage_eq_of_subsetâŸ©
#align set.image_preimage_eq_iff Set.image_preimage_eq_iff

theorem subset_range_iff_exists_image_eq {f : Î± â†’ Î²} {s : Set Î²} : s âŠ† range f â†” âˆƒ t, f '' t = s :=
  âŸ¨fun h => âŸ¨_, image_preimage_eq_iff.2 hâŸ©, fun âŸ¨_, htâŸ© => ht â–¸ image_subset_range _ _âŸ©
#align set.subset_range_iff_exists_image_eq Set.subset_range_iff_exists_image_eq

theorem range_image (f : Î± â†’ Î²) : range (image f) = ğ’« range f :=
  ext fun _ => subset_range_iff_exists_image_eq.symm
#align set.range_image Set.range_image

@[simp]
theorem exists_subset_range_and_iff {f : Î± â†’ Î²} {p : Set Î² â†’ Prop} :
    (âˆƒ s, s âŠ† range f âˆ§ p s) â†” âˆƒ s, p (f '' s) := by
  rw [â† exists_range_iff, range_image]; rfl
#align set.exists_subset_range_and_iff Set.exists_subset_range_and_iff

theorem exists_subset_range_iff {f : Î± â†’ Î²} {p : Set Î² â†’ Prop} :
    (âˆƒ (s : _) (_ : s âŠ† range f), p s) â†” âˆƒ s, p (f '' s) := by simp
#align set.exists_subset_range_iff Set.exists_subset_range_iff

theorem forall_subset_range_iff {f : Î± â†’ Î²} {p : Set Î² â†’ Prop} :
    (âˆ€ s, s âŠ† range f â†’ p s) â†” âˆ€ s, p (f '' s) := by
  rw [â† forall_mem_range, range_image]; rfl

@[simp]
theorem preimage_subset_preimage_iff {s t : Set Î±} {f : Î² â†’ Î±} (hs : s âŠ† range f) :
    f â»Â¹' s âŠ† f â»Â¹' t â†” s âŠ† t := by
  constructor
  Â· intro h x hx
    rcases hs hx with âŸ¨y, rflâŸ©
    exact h hx
  intro h x; apply h
#align set.preimage_subset_preimage_iff Set.preimage_subset_preimage_iff

theorem preimage_eq_preimage' {s t : Set Î±} {f : Î² â†’ Î±} (hs : s âŠ† range f) (ht : t âŠ† range f) :
    f â»Â¹' s = f â»Â¹' t â†” s = t := by
  constructor
  Â· intro h
    apply Subset.antisymm
    Â· rw [â† preimage_subset_preimage_iff hs, h]
    Â· rw [â† preimage_subset_preimage_iff ht, h]
  rintro rfl; rfl
#align set.preimage_eq_preimage' Set.preimage_eq_preimage'

-- Porting note:
-- @[simp] `simp` can prove this
theorem preimage_inter_range {f : Î± â†’ Î²} {s : Set Î²} : f â»Â¹' (s âˆ© range f) = f â»Â¹' s :=
  Set.ext fun x => and_iff_left âŸ¨x, rflâŸ©
#align set.preimage_inter_range Set.preimage_inter_range

-- Porting note:
-- @[simp] `simp` can prove this
theorem preimage_range_inter {f : Î± â†’ Î²} {s : Set Î²} : f â»Â¹' (range f âˆ© s) = f â»Â¹' s := by
  rw [inter_comm, preimage_inter_range]
#align set.preimage_range_inter Set.preimage_range_inter

theorem preimage_image_preimage {f : Î± â†’ Î²} {s : Set Î²} : f â»Â¹' (f '' (f â»Â¹' s)) = f â»Â¹' s := by
  rw [image_preimage_eq_range_inter, preimage_range_inter]
#align set.preimage_image_preimage Set.preimage_image_preimage

@[simp, mfld_simps]
theorem range_id : range (@id Î±) = univ :=
  range_iff_surjective.2 surjective_id
#align set.range_id Set.range_id

@[simp, mfld_simps]
theorem range_id' : (range fun x : Î± => x) = univ :=
  range_id
#align set.range_id' Set.range_id'

@[simp]
theorem _root_.Prod.range_fst [Nonempty Î²] : range (Prod.fst : Î± Ã— Î² â†’ Î±) = univ :=
  Prod.fst_surjective.range_eq
#align prod.range_fst Prod.range_fst

@[simp]
theorem _root_.Prod.range_snd [Nonempty Î±] : range (Prod.snd : Î± Ã— Î² â†’ Î²) = univ :=
  Prod.snd_surjective.range_eq
#align prod.range_snd Prod.range_snd

@[simp]
theorem range_eval {Î± : Î¹ â†’ Sort _} [âˆ€ i, Nonempty (Î± i)] (i : Î¹) :
    range (eval i : (âˆ€ i, Î± i) â†’ Î± i) = univ :=
  (surjective_eval i).range_eq
#align set.range_eval Set.range_eval

theorem range_inl : range (@Sum.inl Î± Î²) = {x | Sum.isLeft x} := by ext (_|_) <;> simp
#align set.range_inl Set.range_inl
theorem range_inr : range (@Sum.inr Î± Î²) = {x | Sum.isRight x} := by ext (_|_) <;> simp
#align set.range_inr Set.range_inr

theorem isCompl_range_inl_range_inr : IsCompl (range <| @Sum.inl Î± Î²) (range Sum.inr) :=
  IsCompl.of_le
    (by
      rintro y âŸ¨âŸ¨xâ‚, rflâŸ©, âŸ¨xâ‚‚, hâŸ©âŸ©
      exact Sum.noConfusion h)
    (by rintro (x | y) - <;> [left; right] <;> exact mem_range_self _)
#align set.is_compl_range_inl_range_inr Set.isCompl_range_inl_range_inr

@[simp]
theorem range_inl_union_range_inr : range (Sum.inl : Î± â†’ Sum Î± Î²) âˆª range Sum.inr = univ :=
  isCompl_range_inl_range_inr.sup_eq_top
#align set.range_inl_union_range_inr Set.range_inl_union_range_inr

@[simp]
theorem range_inl_inter_range_inr : range (Sum.inl : Î± â†’ Sum Î± Î²) âˆ© range Sum.inr = âˆ… :=
  isCompl_range_inl_range_inr.inf_eq_bot
#align set.range_inl_inter_range_inr Set.range_inl_inter_range_inr

@[simp]
theorem range_inr_union_range_inl : range (Sum.inr : Î² â†’ Sum Î± Î²) âˆª range Sum.inl = univ :=
  isCompl_range_inl_range_inr.symm.sup_eq_top
#align set.range_inr_union_range_inl Set.range_inr_union_range_inl

@[simp]
theorem range_inr_inter_range_inl : range (Sum.inr : Î² â†’ Sum Î± Î²) âˆ© range Sum.inl = âˆ… :=
  isCompl_range_inl_range_inr.symm.inf_eq_bot
#align set.range_inr_inter_range_inl Set.range_inr_inter_range_inl

@[simp]
theorem preimage_inl_image_inr (s : Set Î²) : Sum.inl â»Â¹' (@Sum.inr Î± Î² '' s) = âˆ… := by
  ext
  simp
#align set.preimage_inl_image_inr Set.preimage_inl_image_inr

@[simp]
theorem preimage_inr_image_inl (s : Set Î±) : Sum.inr â»Â¹' (@Sum.inl Î± Î² '' s) = âˆ… := by
  ext
  simp
#align set.preimage_inr_image_inl Set.preimage_inr_image_inl

@[simp]
theorem preimage_inl_range_inr : Sum.inl â»Â¹' range (Sum.inr : Î² â†’ Sum Î± Î²) = âˆ… := by
  rw [â† image_univ, preimage_inl_image_inr]
#align set.preimage_inl_range_inr Set.preimage_inl_range_inr

@[simp]
theorem preimage_inr_range_inl : Sum.inr â»Â¹' range (Sum.inl : Î± â†’ Sum Î± Î²) = âˆ… := by
  rw [â† image_univ, preimage_inr_image_inl]
#align set.preimage_inr_range_inl Set.preimage_inr_range_inl

@[simp]
theorem compl_range_inl : (range (Sum.inl : Î± â†’ Sum Î± Î²))á¶œ = range (Sum.inr : Î² â†’ Sum Î± Î²) :=
  IsCompl.compl_eq isCompl_range_inl_range_inr
#align set.compl_range_inl Set.compl_range_inl

@[simp]
theorem compl_range_inr : (range (Sum.inr : Î² â†’ Sum Î± Î²))á¶œ = range (Sum.inl : Î± â†’ Sum Î± Î²) :=
  IsCompl.compl_eq isCompl_range_inl_range_inr.symm
#align set.compl_range_inr Set.compl_range_inr

theorem image_preimage_inl_union_image_preimage_inr (s : Set (Sum Î± Î²)) :
    Sum.inl '' (Sum.inl â»Â¹' s) âˆª Sum.inr '' (Sum.inr â»Â¹' s) = s := by
  rw [image_preimage_eq_inter_range, image_preimage_eq_inter_range, â† inter_union_distrib_left,
    range_inl_union_range_inr, inter_univ]
#align set.image_preimage_inl_union_image_preimage_inr Set.image_preimage_inl_union_image_preimage_inr

@[simp]
theorem range_quot_mk (r : Î± â†’ Î± â†’ Prop) : range (Quot.mk r) = univ :=
  (surjective_quot_mk r).range_eq
#align set.range_quot_mk Set.range_quot_mk

@[simp]
theorem range_quot_lift {r : Î¹ â†’ Î¹ â†’ Prop} (hf : âˆ€ x y, r x y â†’ f x = f y) :
    range (Quot.lift f hf) = range f :=
  ext fun _ => (surjective_quot_mk _).exists
#align set.range_quot_lift Set.range_quot_lift

-- Porting note: the `Setoid Î±` instance is not being filled in
@[simp]
theorem range_quotient_mk [sa : Setoid Î±] : (range (Î± := Quotient sa) fun x : Î± => âŸ¦xâŸ§) = univ :=
  range_quot_mk _
#align set.range_quotient_mk Set.range_quotient_mk

@[simp]
theorem range_quotient_lift [s : Setoid Î¹] (hf) :
    range (Quotient.lift f hf : Quotient s â†’ Î±) = range f :=
  range_quot_lift _
#align set.range_quotient_lift Set.range_quotient_lift

@[simp]
theorem range_quotient_mk' {s : Setoid Î±} : range (Quotient.mk' : Î± â†’ Quotient s) = univ :=
  range_quot_mk _
#align set.range_quotient_mk' Set.range_quotient_mk'

@[simp] lemma Quotient.range_mk'' {sa : Setoid Î±} : range (Quotient.mk'' (sâ‚ := sa)) = univ :=
  range_quotient_mk

@[simp]
theorem range_quotient_lift_on' {s : Setoid Î¹} (hf) :
    (range fun x : Quotient s => Quotient.liftOn' x f hf) = range f :=
  range_quot_lift _
#align set.range_quotient_lift_on' Set.range_quotient_lift_on'

instance canLift (c) (p) [CanLift Î± Î² c p] :
    CanLift (Set Î±) (Set Î²) (c '' Â·) fun s => âˆ€ x âˆˆ s, p x where
  prf _ hs := subset_range_iff_exists_image_eq.mp fun x hx => CanLift.prf _ (hs x hx)
#align set.can_lift Set.canLift

theorem range_const_subset {c : Î±} : (range fun _ : Î¹ => c) âŠ† {c} :=
  range_subset_iff.2 fun _ => rfl
#align set.range_const_subset Set.range_const_subset

@[simp]
theorem range_const : âˆ€ [Nonempty Î¹] {c : Î±}, (range fun _ : Î¹ => c) = {c}
  | âŸ¨xâŸ©, _ =>
    (Subset.antisymm range_const_subset) fun _ hy =>
      (mem_singleton_iff.1 hy).symm â–¸ mem_range_self x
#align set.range_const Set.range_const

theorem range_subtype_map {p : Î± â†’ Prop} {q : Î² â†’ Prop} (f : Î± â†’ Î²) (h : âˆ€ x, p x â†’ q (f x)) :
    range (Subtype.map f h) = (â†‘) â»Â¹' (f '' { x | p x }) := by
  ext âŸ¨x, hxâŸ©
  rw [mem_preimage, mem_range, mem_image, Subtype.exists, Subtype.coe_mk]
  apply Iff.intro
  Â· rintro âŸ¨a, b, habâŸ©
    rw [Subtype.map, Subtype.mk.injEq] at hab
    use a
    trivial
  Â· rintro âŸ¨a, b, habâŸ©
    use a
    use b
    rw [Subtype.map, Subtype.mk.injEq]
    exact hab
  -- Porting note: `simp_rw` fails here
  -- simp_rw [mem_preimage, mem_range, mem_image, Subtype.exists, Subtype.map, Subtype.coe_mk,
  --   mem_set_of, exists_prop]
#align set.range_subtype_map Set.range_subtype_map

theorem image_swap_eq_preimage_swap : image (@Prod.swap Î± Î²) = preimage Prod.swap :=
  image_eq_preimage_of_inverse Prod.swap_leftInverse Prod.swap_rightInverse
#align set.image_swap_eq_preimage_swap Set.image_swap_eq_preimage_swap

theorem preimage_singleton_nonempty {f : Î± â†’ Î²} {y : Î²} : (f â»Â¹' {y}).Nonempty â†” y âˆˆ range f :=
  Iff.rfl
#align set.preimage_singleton_nonempty Set.preimage_singleton_nonempty

theorem preimage_singleton_eq_empty {f : Î± â†’ Î²} {y : Î²} : f â»Â¹' {y} = âˆ… â†” y âˆ‰ range f :=
  not_nonempty_iff_eq_empty.symm.trans preimage_singleton_nonempty.not
#align set.preimage_singleton_eq_empty Set.preimage_singleton_eq_empty

theorem range_subset_singleton {f : Î¹ â†’ Î±} {x : Î±} : range f âŠ† {x} â†” f = const Î¹ x := by
  simp [range_subset_iff, funext_iff, mem_singleton]
#align set.range_subset_singleton Set.range_subset_singleton

theorem image_compl_preimage {f : Î± â†’ Î²} {s : Set Î²} : f '' (f â»Â¹' s)á¶œ = range f \ s := by
  rw [compl_eq_univ_diff, image_diff_preimage, image_univ]
#align set.image_compl_preimage Set.image_compl_preimage

theorem rangeFactorization_eq {f : Î¹ â†’ Î²} : Subtype.val âˆ˜ rangeFactorization f = f :=
  funext fun _ => rfl
#align set.range_factorization_eq Set.rangeFactorization_eq

@[simp]
theorem rangeFactorization_coe (f : Î¹ â†’ Î²) (a : Î¹) : (rangeFactorization f a : Î²) = f a :=
  rfl
#align set.range_factorization_coe Set.rangeFactorization_coe

@[simp]
theorem coe_comp_rangeFactorization (f : Î¹ â†’ Î²) : (â†‘) âˆ˜ rangeFactorization f = f := rfl
#align set.coe_comp_range_factorization Set.coe_comp_rangeFactorization

theorem surjective_onto_range : Surjective (rangeFactorization f) := fun âŸ¨_, âŸ¨i, rflâŸ©âŸ© => âŸ¨i, rflâŸ©
#align set.surjective_onto_range Set.surjective_onto_range

theorem image_eq_range (f : Î± â†’ Î²) (s : Set Î±) : f '' s = range fun x : s => f x := by
  ext
  constructor
  Â· rintro âŸ¨x, h1, h2âŸ©
    exact âŸ¨âŸ¨x, h1âŸ©, h2âŸ©
  Â· rintro âŸ¨âŸ¨x, h1âŸ©, h2âŸ©
    exact âŸ¨x, h1, h2âŸ©
#align set.image_eq_range Set.image_eq_range

theorem _root_.Sum.range_eq (f : Sum Î± Î² â†’ Î³) :
    range f = range (f âˆ˜ Sum.inl) âˆª range (f âˆ˜ Sum.inr) :=
  ext fun _ => Sum.exists
#align sum.range_eq Sum.range_eq

@[simp]
theorem Sum.elim_range (f : Î± â†’ Î³) (g : Î² â†’ Î³) : range (Sum.elim f g) = range f âˆª range g :=
  Sum.range_eq _
#align set.sum.elim_range Set.Sum.elim_range

theorem range_ite_subset' {p : Prop} [Decidable p] {f g : Î± â†’ Î²} :
    range (if p then f else g) âŠ† range f âˆª range g := by
  by_cases h : p
  Â· rw [if_pos h]
    exact subset_union_left
  Â· rw [if_neg h]
    exact subset_union_right
#align set.range_ite_subset' Set.range_ite_subset'

theorem range_ite_subset {p : Î± â†’ Prop} [DecidablePred p] {f g : Î± â†’ Î²} :
    (range fun x => if p x then f x else g x) âŠ† range f âˆª range g := by
  rw [range_subset_iff]; intro x; by_cases h : p x
  Â· simp only [if_pos h, mem_union, mem_range, exists_apply_eq_apply, true_or]
  Â· simp [if_neg h, mem_union, mem_range_self]
#align set.range_ite_subset Set.range_ite_subset

@[simp]
theorem preimage_range (f : Î± â†’ Î²) : f â»Â¹' range f = univ :=
  eq_univ_of_forall mem_range_self
#align set.preimage_range Set.preimage_range

/-- The range of a function from a `Unique` type contains just the
function applied to its single value. -/
theorem range_unique [h : Unique Î¹] : range f = {f default} := by
  ext x
  rw [mem_range]
  constructor
  Â· rintro âŸ¨i, hiâŸ©
    rw [h.uniq i] at hi
    exact hi â–¸ mem_singleton _
  Â· exact fun h => âŸ¨default, h.symmâŸ©
#align set.range_unique Set.range_unique

theorem range_diff_image_subset (f : Î± â†’ Î²) (s : Set Î±) : range f \ f '' s âŠ† f '' sá¶œ :=
  fun _ âŸ¨âŸ¨x, hâ‚âŸ©, hâ‚‚âŸ© => âŸ¨x, fun h => hâ‚‚ âŸ¨x, h, hâ‚âŸ©, hâ‚âŸ©
#align set.range_diff_image_subset Set.range_diff_image_subset

theorem range_diff_image {f : Î± â†’ Î²} (H : Injective f) (s : Set Î±) : range f \ f '' s = f '' sá¶œ :=
  (Subset.antisymm (range_diff_image_subset f s)) fun _ âŸ¨_, hx, hyâŸ© =>
    hy â–¸ âŸ¨mem_range_self _, fun âŸ¨_, hx', EqâŸ© => hx <| H Eq â–¸ hx'âŸ©
#align set.range_diff_image Set.range_diff_image

@[simp]
theorem range_inclusion (h : s âŠ† t) : range (inclusion h) = { x : t | (x : Î±) âˆˆ s } := by
  ext âŸ¨x, hxâŸ©
  -- Porting note: `simp [inclusion]` doesn't solve goal
  apply Iff.intro
  Â· rw [mem_range]
    rintro âŸ¨a, haâŸ©
    rw [inclusion, Subtype.mk.injEq] at ha
    rw [mem_setOf, Subtype.coe_mk, â† ha]
    exact Subtype.coe_prop _
  Â· rw [mem_setOf, Subtype.coe_mk, mem_range]
    intro hx'
    use âŸ¨x, hx'âŸ©
    trivial
  -- simp_rw [inclusion, mem_range, Subtype.mk_eq_mk]
  -- rw [SetCoe.exists, Subtype.coe_mk, exists_prop, exists_eq_right, mem_set_of, Subtype.coe_mk]
#align set.range_inclusion Set.range_inclusion

-- When `f` is injective, see also `Equiv.ofInjective`.
theorem leftInverse_rangeSplitting (f : Î± â†’ Î²) :
    LeftInverse (rangeFactorization f) (rangeSplitting f) := fun x => by
  apply Subtype.ext -- Porting note: why doesn't `ext` find this lemma?
  simp only [rangeFactorization_coe]
  apply apply_rangeSplitting
#align set.left_inverse_range_splitting Set.leftInverse_rangeSplitting

theorem rangeSplitting_injective (f : Î± â†’ Î²) : Injective (rangeSplitting f) :=
  (leftInverse_rangeSplitting f).injective
#align set.range_splitting_injective Set.rangeSplitting_injective

theorem rightInverse_rangeSplitting {f : Î± â†’ Î²} (h : Injective f) :
    RightInverse (rangeFactorization f) (rangeSplitting f) :=
  (leftInverse_rangeSplitting f).rightInverse_of_injective fun _ _ hxy =>
    h <| Subtype.ext_iff.1 hxy
#align set.right_inverse_range_splitting Set.rightInverse_rangeSplitting

theorem preimage_rangeSplitting {f : Î± â†’ Î²} (hf : Injective f) :
    preimage (rangeSplitting f) = image (rangeFactorization f) :=
  (image_eq_preimage_of_inverse (rightInverse_rangeSplitting hf)
      (leftInverse_rangeSplitting f)).symm
#align set.preimage_range_splitting Set.preimage_rangeSplitting

theorem isCompl_range_some_none (Î± : Type*) : IsCompl (range (some : Î± â†’ Option Î±)) {none} :=
  IsCompl.of_le (fun _ âŸ¨âŸ¨_, haâŸ©, (hn : _ = none)âŸ© => Option.some_ne_none _ (ha.trans hn))
    fun x _ => Option.casesOn x (Or.inr rfl) fun _ => Or.inl <| mem_range_self _
#align set.is_compl_range_some_none Set.isCompl_range_some_none

@[simp]
theorem compl_range_some (Î± : Type*) : (range (some : Î± â†’ Option Î±))á¶œ = {none} :=
  (isCompl_range_some_none Î±).compl_eq
#align set.compl_range_some Set.compl_range_some

@[simp]
theorem range_some_inter_none (Î± : Type*) : range (some : Î± â†’ Option Î±) âˆ© {none} = âˆ… :=
  (isCompl_range_some_none Î±).inf_eq_bot
#align set.range_some_inter_none Set.range_some_inter_none

-- Porting note:
-- @[simp] `simp` can prove this
theorem range_some_union_none (Î± : Type*) : range (some : Î± â†’ Option Î±) âˆª {none} = univ :=
  (isCompl_range_some_none Î±).sup_eq_top
#align set.range_some_union_none Set.range_some_union_none

@[simp]
theorem insert_none_range_some (Î± : Type*) : insert none (range (some : Î± â†’ Option Î±)) = univ :=
  (isCompl_range_some_none Î±).symm.sup_eq_top
#align set.insert_none_range_some Set.insert_none_range_some

end Range

section Subsingleton

variable {s : Set Î±}

/-- The image of a subsingleton is a subsingleton. -/
theorem Subsingleton.image (hs : s.Subsingleton) (f : Î± â†’ Î²) : (f '' s).Subsingleton :=
  fun _ âŸ¨_, hx, HxâŸ© _ âŸ¨_, hy, HyâŸ© => Hx â–¸ Hy â–¸ congr_arg f (hs hx hy)
#align set.subsingleton.image Set.Subsingleton.image

/-- The preimage of a subsingleton under an injective map is a subsingleton. -/
theorem Subsingleton.preimage {s : Set Î²} (hs : s.Subsingleton) {f : Î± â†’ Î²}
    (hf : Function.Injective f) : (f â»Â¹' s).Subsingleton := fun _ ha _ hb => hf <| hs ha hb
#align set.subsingleton.preimage Set.Subsingleton.preimage

/-- If the image of a set under an injective map is a subsingleton, the set is a subsingleton. -/
theorem subsingleton_of_image {f : Î± â†’ Î²} (hf : Function.Injective f) (s : Set Î±)
    (hs : (f '' s).Subsingleton) : s.Subsingleton :=
  (hs.preimage hf).anti <| subset_preimage_image _ _
#align set.subsingleton_of_image Set.subsingleton_of_image

/-- If the preimage of a set under a surjective map is a subsingleton,
the set is a subsingleton. -/
theorem subsingleton_of_preimage {f : Î± â†’ Î²} (hf : Function.Surjective f) (s : Set Î²)
    (hs : (f â»Â¹' s).Subsingleton) : s.Subsingleton := fun fx hx fy hy => by
  rcases hf fx, hf fy with âŸ¨âŸ¨x, rflâŸ©, âŸ¨y, rflâŸ©âŸ©
  exact congr_arg f (hs hx hy)
#align set.subsingleton_of_preimage Set.subsingleton_of_preimage

theorem subsingleton_range {Î± : Sort*} [Subsingleton Î±] (f : Î± â†’ Î²) : (range f).Subsingleton :=
  forall_mem_range.2 fun x => forall_mem_range.2 fun y => congr_arg f (Subsingleton.elim x y)
#align set.subsingleton_range Set.subsingleton_range

/-- The preimage of a nontrivial set under a surjective map is nontrivial. -/
theorem Nontrivial.preimage {s : Set Î²} (hs : s.Nontrivial) {f : Î± â†’ Î²}
    (hf : Function.Surjective f) : (f â»Â¹' s).Nontrivial := by
  rcases hs with âŸ¨fx, hx, fy, hy, hxyâŸ©
  rcases hf fx, hf fy with âŸ¨âŸ¨x, rflâŸ©, âŸ¨y, rflâŸ©âŸ©
  exact âŸ¨x, hx, y, hy, mt (congr_arg f) hxyâŸ©
#align set.nontrivial.preimage Set.Nontrivial.preimage

/-- The image of a nontrivial set under an injective map is nontrivial. -/
theorem Nontrivial.image (hs : s.Nontrivial) {f : Î± â†’ Î²} (hf : Function.Injective f) :
    (f '' s).Nontrivial :=
  let âŸ¨x, hx, y, hy, hxyâŸ© := hs
  âŸ¨f x, mem_image_of_mem f hx, f y, mem_image_of_mem f hy, hf.ne hxyâŸ©
#align set.nontrivial.image Set.Nontrivial.image

/-- If the image of a set is nontrivial, the set is nontrivial. -/
theorem nontrivial_of_image (f : Î± â†’ Î²) (s : Set Î±) (hs : (f '' s).Nontrivial) : s.Nontrivial :=
  let âŸ¨_, âŸ¨x, hx, rflâŸ©, _, âŸ¨y, hy, rflâŸ©, hxyâŸ© := hs
  âŸ¨x, hx, y, hy, mt (congr_arg f) hxyâŸ©
#align set.nontrivial_of_image Set.nontrivial_of_image

@[simp]
theorem image_nontrivial {f : Î± â†’ Î²} (hf : f.Injective) : (f '' s).Nontrivial â†” s.Nontrivial :=
  âŸ¨nontrivial_of_image f s, fun h â†¦ h.image hfâŸ©

/-- If the preimage of a set under an injective map is nontrivial, the set is nontrivial. -/
theorem nontrivial_of_preimage {f : Î± â†’ Î²} (hf : Function.Injective f) (s : Set Î²)
    (hs : (f â»Â¹' s).Nontrivial) : s.Nontrivial :=
  (hs.image hf).mono <| image_preimage_subset _ _
#align set.nontrivial_of_preimage Set.nontrivial_of_preimage

end Subsingleton

end Set

namespace Function

variable {Î± Î² : Type*} {Î¹ : Sort*} {f : Î± â†’ Î²}

open Set

theorem Surjective.preimage_injective (hf : Surjective f) : Injective (preimage f) := fun _ _ =>
  (preimage_eq_preimage hf).1
#align function.surjective.preimage_injective Function.Surjective.preimage_injective

theorem Injective.preimage_image (hf : Injective f) (s : Set Î±) : f â»Â¹' (f '' s) = s :=
  preimage_image_eq s hf
#align function.injective.preimage_image Function.Injective.preimage_image

theorem Injective.preimage_surjective (hf : Injective f) : Surjective (preimage f) := by
  intro s
  use f '' s
  rw [hf.preimage_image]
#align function.injective.preimage_surjective Function.Injective.preimage_surjective

theorem Injective.subsingleton_image_iff (hf : Injective f) {s : Set Î±} :
    (f '' s).Subsingleton â†” s.Subsingleton :=
  âŸ¨subsingleton_of_image hf s, fun h => h.image fâŸ©
#align function.injective.subsingleton_image_iff Function.Injective.subsingleton_image_iff

theorem Surjective.image_preimage (hf : Surjective f) (s : Set Î²) : f '' (f â»Â¹' s) = s :=
  image_preimage_eq s hf
#align function.surjective.image_preimage Function.Surjective.image_preimage

theorem Surjective.image_surjective (hf : Surjective f) : Surjective (image f) := by
  intro s
  use f â»Â¹' s
  rw [hf.image_preimage]
#align function.surjective.image_surjective Function.Surjective.image_surjective

@[simp]
theorem Surjective.nonempty_preimage (hf : Surjective f) {s : Set Î²} :
    (f â»Â¹' s).Nonempty â†” s.Nonempty := by rw [â† image_nonempty, hf.image_preimage]
#align function.surjective.nonempty_preimage Function.Surjective.nonempty_preimage

theorem Injective.image_injective (hf : Injective f) : Injective (image f) := by
  intro s t h
  rw [â† preimage_image_eq s hf, â† preimage_image_eq t hf, h]
#align function.injective.image_injective Function.Injective.image_injective

theorem Surjective.preimage_subset_preimage_iff {s t : Set Î²} (hf : Surjective f) :
    f â»Â¹' s âŠ† f â»Â¹' t â†” s âŠ† t := by
  apply Set.preimage_subset_preimage_iff
  rw [hf.range_eq]
  apply subset_univ
#align function.surjective.preimage_subset_preimage_iff Function.Surjective.preimage_subset_preimage_iff

theorem Surjective.range_comp {Î¹' : Sort*} {f : Î¹ â†’ Î¹'} (hf : Surjective f) (g : Î¹' â†’ Î±) :
    range (g âˆ˜ f) = range g :=
  ext fun y => (@Surjective.exists _ _ _ hf fun x => g x = y).symm
#align function.surjective.range_comp Function.Surjective.range_comp

theorem Injective.mem_range_iff_exists_unique (hf : Injective f) {b : Î²} :
    b âˆˆ range f â†” âˆƒ! a, f a = b :=
  âŸ¨fun âŸ¨a, hâŸ© => âŸ¨a, h, fun _ ha => hf (ha.trans h.symm)âŸ©, ExistsUnique.existsâŸ©
#align function.injective.mem_range_iff_exists_unique Function.Injective.mem_range_iff_exists_unique

theorem Injective.exists_unique_of_mem_range (hf : Injective f) {b : Î²} (hb : b âˆˆ range f) :
    âˆƒ! a, f a = b :=
  hf.mem_range_iff_exists_unique.mp hb
#align function.injective.exists_unique_of_mem_range Function.Injective.exists_unique_of_mem_range

theorem Injective.compl_image_eq (hf : Injective f) (s : Set Î±) :
    (f '' s)á¶œ = f '' sá¶œ âˆª (range f)á¶œ := by
  ext y
  rcases em (y âˆˆ range f) with (âŸ¨x, rflâŸ© | hx)
  Â· simp [hf.eq_iff]
  Â· rw [mem_range, not_exists] at hx
    simp [hx]
#align function.injective.compl_image_eq Function.Injective.compl_image_eq

theorem LeftInverse.image_image {g : Î² â†’ Î±} (h : LeftInverse g f) (s : Set Î±) :
    g '' (f '' s) = s := by rw [â† image_comp, h.comp_eq_id, image_id]
#align function.left_inverse.image_image Function.LeftInverse.image_image

theorem LeftInverse.preimage_preimage {g : Î² â†’ Î±} (h : LeftInverse g f) (s : Set Î±) :
    f â»Â¹' (g â»Â¹' s) = s := by rw [â† preimage_comp, h.comp_eq_id, preimage_id]
#align function.left_inverse.preimage_preimage Function.LeftInverse.preimage_preimage

protected theorem Involutive.preimage {f : Î± â†’ Î±} (hf : Involutive f) : Involutive (preimage f) :=
  hf.rightInverse.preimage_preimage
#align function.involutive.preimage Function.Involutive.preimage

end Function

namespace EquivLike

variable {Î¹ Î¹' : Sort*} {E : Type*} [EquivLike E Î¹ Î¹']

@[simp] lemma range_comp {Î± : Type*} (f : Î¹' â†’ Î±) (e : E) : range (f âˆ˜ e) = range f :=
  (EquivLike.surjective _).range_comp _
#align equiv_like.range_comp EquivLike.range_comp

end EquivLike

/-! ### Image and preimage on subtypes -/


namespace Subtype

variable {Î± : Type*}

theorem coe_image {p : Î± â†’ Prop} {s : Set (Subtype p)} :
    (â†‘) '' s = { x | âˆƒ h : p x, (âŸ¨x, hâŸ© : Subtype p) âˆˆ s } :=
  Set.ext fun a =>
    âŸ¨fun âŸ¨âŸ¨_, ha'âŸ©, in_s, h_eqâŸ© => h_eq â–¸ âŸ¨ha', in_sâŸ©, fun âŸ¨ha, in_sâŸ© => âŸ¨âŸ¨a, haâŸ©, in_s, rflâŸ©âŸ©
#align subtype.coe_image Subtype.coe_image

@[simp]
theorem coe_image_of_subset {s t : Set Î±} (h : t âŠ† s) : (â†‘) '' { x : â†¥s | â†‘x âˆˆ t } = t := by
  ext x
  rw [mem_image]
  exact âŸ¨fun âŸ¨_, hx', hxâŸ© => hx â–¸ hx', fun hx => âŸ¨âŸ¨x, h hxâŸ©, hx, rflâŸ©âŸ©
#align subtype.coe_image_of_subset Subtype.coe_image_of_subset

theorem range_coe {s : Set Î±} : range ((â†‘) : s â†’ Î±) = s := by
  rw [â† image_univ]
  simp [-image_univ, coe_image]
#align subtype.range_coe Subtype.range_coe

/-- A variant of `range_coe`. Try to use `range_coe` if possible.
  This version is useful when defining a new type that is defined as the subtype of something.
  In that case, the coercion doesn't fire anymore. -/
theorem range_val {s : Set Î±} : range (Subtype.val : s â†’ Î±) = s :=
  range_coe
#align subtype.range_val Subtype.range_val

/-- We make this the simp lemma instead of `range_coe`. The reason is that if we write
  for `s : Set Î±` the function `(â†‘) : s â†’ Î±`, then the inferred implicit arguments of `(â†‘)` are
  `â†‘Î± (fun x â†¦ x âˆˆ s)`. -/
@[simp]
theorem range_coe_subtype {p : Î± â†’ Prop} : range ((â†‘) : Subtype p â†’ Î±) = { x | p x } :=
  range_coe
#align subtype.range_coe_subtype Subtype.range_coe_subtype

@[simp]
theorem coe_preimage_self (s : Set Î±) : ((â†‘) : s â†’ Î±) â»Â¹' s = univ := by
  rw [â† preimage_range, range_coe]
#align subtype.coe_preimage_self Subtype.coe_preimage_self

theorem range_val_subtype {p : Î± â†’ Prop} : range (Subtype.val : Subtype p â†’ Î±) = { x | p x } :=
  range_coe
#align subtype.range_val_subtype Subtype.range_val_subtype

theorem coe_image_subset (s : Set Î±) (t : Set s) : ((â†‘) : s â†’ Î±) '' t âŠ† s :=
  fun x âŸ¨y, _, yvaleqâŸ© => by
  rw [â† yvaleq]; exact y.property
#align subtype.coe_image_subset Subtype.coe_image_subset

theorem coe_image_univ (s : Set Î±) : ((â†‘) : s â†’ Î±) '' Set.univ = s :=
  image_univ.trans range_coe
#align subtype.coe_image_univ Subtype.coe_image_univ

@[simp]
theorem image_preimage_coe (s t : Set Î±) : ((â†‘) : s â†’ Î±) '' (((â†‘) : s â†’ Î±) â»Â¹' t) = s âˆ© t :=
  image_preimage_eq_range_inter.trans <| congr_arg (Â· âˆ© t) range_coe
#align subtype.image_preimage_coe Subtype.image_preimage_coe

theorem image_preimage_val (s t : Set Î±) : (Subtype.val : s â†’ Î±) '' (Subtype.val â»Â¹' t) = s âˆ© t :=
  image_preimage_coe s t
#align subtype.image_preimage_val Subtype.image_preimage_val

theorem preimage_coe_eq_preimage_coe_iff {s t u : Set Î±} :
    ((â†‘) : s â†’ Î±) â»Â¹' t = ((â†‘) : s â†’ Î±) â»Â¹' u â†” s âˆ© t = s âˆ© u := by
  rw [â† image_preimage_coe, â† image_preimage_coe, coe_injective.image_injective.eq_iff]
#align subtype.preimage_coe_eq_preimage_coe_iff Subtype.preimage_coe_eq_preimage_coe_iff

theorem preimage_coe_self_inter (s t : Set Î±) :
    ((â†‘) : s â†’ Î±) â»Â¹' (s âˆ© t) = ((â†‘) : s â†’ Î±) â»Â¹' t := by
  rw [preimage_coe_eq_preimage_coe_iff, â† inter_assoc, inter_self]

-- Porting note:
-- @[simp] `simp` can prove this
theorem preimage_coe_inter_self (s t : Set Î±) :
    ((â†‘) : s â†’ Î±) â»Â¹' (t âˆ© s) = ((â†‘) : s â†’ Î±) â»Â¹' t := by
  rw [inter_comm, preimage_coe_self_inter]
#align subtype.preimage_coe_inter_self Subtype.preimage_coe_inter_self

theorem preimage_val_eq_preimage_val_iff (s t u : Set Î±) :
    (Subtype.val : s â†’ Î±) â»Â¹' t = Subtype.val â»Â¹' u â†” s âˆ© t = s âˆ© u :=
  preimage_coe_eq_preimage_coe_iff
#align subtype.preimage_val_eq_preimage_val_iff Subtype.preimage_val_eq_preimage_val_iff

lemma preimage_val_subset_preimage_val_iff (s t u : Set Î±) :
    (Subtype.val â»Â¹' t : Set s) âŠ† Subtype.val â»Â¹' u â†” s âˆ© t âŠ† s âˆ© u := by
  constructor
  Â· rw [â† image_preimage_coe, â† image_preimage_coe]
    exact image_subset _
  Â· intro h x a
    exact (h âŸ¨x.2, aâŸ©).2

theorem exists_set_subtype {t : Set Î±} (p : Set Î± â†’ Prop) :
    (âˆƒ s : Set t, p (((â†‘) : t â†’ Î±) '' s)) â†” âˆƒ s : Set Î±, s âŠ† t âˆ§ p s := by
  rw [â† exists_subset_range_and_iff, range_coe]
#align subtype.exists_set_subtype Subtype.exists_set_subtype

theorem forall_set_subtype {t : Set Î±} (p : Set Î± â†’ Prop) :
    (âˆ€ s : Set t, p (((â†‘) : t â†’ Î±) '' s)) â†” âˆ€ s : Set Î±, s âŠ† t â†’ p s := by
  rw [â† forall_subset_range_iff, range_coe]

theorem preimage_coe_nonempty {s t : Set Î±} :
    (((â†‘) : s â†’ Î±) â»Â¹' t).Nonempty â†” (s âˆ© t).Nonempty := by
  rw [â† image_preimage_coe, image_nonempty]
#align subtype.preimage_coe_nonempty Subtype.preimage_coe_nonempty

theorem preimage_coe_eq_empty {s t : Set Î±} : ((â†‘) : s â†’ Î±) â»Â¹' t = âˆ… â†” s âˆ© t = âˆ… := by
  simp [â† not_nonempty_iff_eq_empty, preimage_coe_nonempty]
#align subtype.preimage_coe_eq_empty Subtype.preimage_coe_eq_empty

-- Porting note:
-- @[simp] `simp` can prove this
theorem preimage_coe_compl (s : Set Î±) : ((â†‘) : s â†’ Î±) â»Â¹' sá¶œ = âˆ… :=
  preimage_coe_eq_empty.2 (inter_compl_self s)
#align subtype.preimage_coe_compl Subtype.preimage_coe_compl

@[simp]
theorem preimage_coe_compl' (s : Set Î±) :
    (fun x : (sá¶œ : Set Î±) => (x : Î±)) â»Â¹' s = âˆ… :=
  preimage_coe_eq_empty.2 (compl_inter_self s)
#align subtype.preimage_coe_compl' Subtype.preimage_coe_compl'

end Subtype

/-! ### Images and preimages on `Option` -/


open Set

namespace Option

theorem injective_iff {Î± Î²} {f : Option Î± â†’ Î²} :
    Injective f â†” Injective (f âˆ˜ some) âˆ§ f none âˆ‰ range (f âˆ˜ some) := by
  simp only [mem_range, not_exists, (Â· âˆ˜ Â·)]
  refine
    âŸ¨fun hf => âŸ¨hf.comp (Option.some_injective _), fun x => hf.ne <| Option.some_ne_none _âŸ©, ?_âŸ©
  rintro âŸ¨h_some, h_noneâŸ© (_ | a) (_ | b) hab
  exacts [rfl, (h_none _ hab.symm).elim, (h_none _ hab).elim, congr_arg some (h_some hab)]
#align option.injective_iff Option.injective_iff

theorem range_eq {Î± Î²} (f : Option Î± â†’ Î²) : range f = insert (f none) (range (f âˆ˜ some)) :=
  Set.ext fun _ => Option.exists.trans <| eq_comm.or Iff.rfl
#align option.range_eq Option.range_eq

end Option

theorem WithBot.range_eq {Î± Î²} (f : WithBot Î± â†’ Î²) :
    range f = insert (f âŠ¥) (range (f âˆ˜ WithBot.some : Î± â†’ Î²)) :=
  Option.range_eq f
#align with_bot.range_eq WithBot.range_eq

theorem WithTop.range_eq {Î± Î²} (f : WithTop Î± â†’ Î²) :
    range f = insert (f âŠ¤) (range (f âˆ˜ WithBot.some : Î± â†’ Î²)) :=
  Option.range_eq f
#align with_top.range_eq WithTop.range_eq

namespace Set

open Function

/-! ### Injectivity and surjectivity lemmas for image and preimage -/


section ImagePreimage

variable {Î± : Type u} {Î² : Type v} {f : Î± â†’ Î²}

@[simp]
theorem preimage_injective : Injective (preimage f) â†” Surjective f := by
  refine âŸ¨fun h y => ?_, Surjective.preimage_injectiveâŸ©
  obtain âŸ¨x, hxâŸ© : (f â»Â¹' {y}).Nonempty := by
    rw [h.nonempty_apply_iff preimage_empty]
    apply singleton_nonempty
  exact âŸ¨x, hxâŸ©
#align set.preimage_injective Set.preimage_injective

@[simp]
theorem preimage_surjective : Surjective (preimage f) â†” Injective f := by
  refine âŸ¨fun h x x' hx => ?_, Injective.preimage_surjectiveâŸ©
  cases' h {x} with s hs; have := mem_singleton x
  rwa [â† hs, mem_preimage, hx, â† mem_preimage, hs, mem_singleton_iff, eq_comm] at this
#align set.preimage_surjective Set.preimage_surjective

@[simp]
theorem image_surjective : Surjective (image f) â†” Surjective f := by
  refine âŸ¨fun h y => ?_, Surjective.image_surjectiveâŸ©
  cases' h {y} with s hs
  have := mem_singleton y; rw [â† hs] at this; rcases this with âŸ¨x, _, hxâŸ©
  exact âŸ¨x, hxâŸ©
#align set.image_surjective Set.image_surjective

@[simp]
theorem image_injective : Injective (image f) â†” Injective f := by
  refine âŸ¨fun h x x' hx => ?_, Injective.image_injectiveâŸ©
  rw [â† singleton_eq_singleton_iff]; apply h
  rw [image_singleton, image_singleton, hx]
#align set.image_injective Set.image_injective

theorem preimage_eq_iff_eq_image {f : Î± â†’ Î²} (hf : Bijective f) {s t} :
    f â»Â¹' s = t â†” s = f '' t := by rw [â† image_eq_image hf.1, hf.2.image_preimage]
#align set.preimage_eq_iff_eq_image Set.preimage_eq_iff_eq_image

theorem eq_preimage_iff_image_eq {f : Î± â†’ Î²} (hf : Bijective f) {s t} :
    s = f â»Â¹' t â†” f '' s = t := by rw [â† image_eq_image hf.1, hf.2.image_preimage]
#align set.eq_preimage_iff_image_eq Set.eq_preimage_iff_image_eq

end ImagePreimage

end Set

/-! ### Disjoint lemmas for image and preimage -/

section Disjoint
variable {Î± Î² Î³ : Type*} {f : Î± â†’ Î²} {s t : Set Î±}

theorem Disjoint.preimage (f : Î± â†’ Î²) {s t : Set Î²} (h : Disjoint s t) :
    Disjoint (f â»Â¹' s) (f â»Â¹' t) :=
  disjoint_iff_inf_le.mpr fun _ hx => h.le_bot hx
#align disjoint.preimage Disjoint.preimage

lemma Codisjoint.preimage (f : Î± â†’ Î²) {s t : Set Î²} (h : Codisjoint s t) :
    Codisjoint (f â»Â¹' s) (f â»Â¹' t) := by
  simp only [codisjoint_iff_le_sup, Set.sup_eq_union, top_le_iff, â† Set.preimage_union] at h âŠ¢
  rw [h]; rfl

lemma IsCompl.preimage (f : Î± â†’ Î²) {s t : Set Î²} (h : IsCompl s t) :
    IsCompl (f â»Â¹' s) (f â»Â¹' t) :=
  âŸ¨h.1.preimage f, h.2.preimage fâŸ©

namespace Set

theorem disjoint_image_image {f : Î² â†’ Î±} {g : Î³ â†’ Î±} {s : Set Î²} {t : Set Î³}
    (h : âˆ€ b âˆˆ s, âˆ€ c âˆˆ t, f b â‰  g c) : Disjoint (f '' s) (g '' t) :=
  disjoint_iff_inf_le.mpr <| by rintro a âŸ¨âŸ¨b, hb, eqâŸ©, c, hc, rflâŸ©; exact h b hb c hc eq
#align set.disjoint_image_image Set.disjoint_image_image

theorem disjoint_image_of_injective (hf : Injective f) {s t : Set Î±} (hd : Disjoint s t) :
    Disjoint (f '' s) (f '' t) :=
  disjoint_image_image fun _ hx _ hy => hf.ne fun H => Set.disjoint_iff.1 hd âŸ¨hx, H.symm â–¸ hyâŸ©
#align set.disjoint_image_of_injective Set.disjoint_image_of_injective

theorem _root_.Disjoint.of_image (h : Disjoint (f '' s) (f '' t)) : Disjoint s t :=
  disjoint_iff_inf_le.mpr fun _ hx =>
    disjoint_left.1 h (mem_image_of_mem _ hx.1) (mem_image_of_mem _ hx.2)
#align disjoint.of_image Disjoint.of_image

@[simp]
theorem disjoint_image_iff (hf : Injective f) : Disjoint (f '' s) (f '' t) â†” Disjoint s t :=
  âŸ¨Disjoint.of_image, disjoint_image_of_injective hfâŸ©
#align set.disjoint_image_iff Set.disjoint_image_iff

theorem _root_.Disjoint.of_preimage (hf : Surjective f) {s t : Set Î²}
    (h : Disjoint (f â»Â¹' s) (f â»Â¹' t)) : Disjoint s t := by
  rw [disjoint_iff_inter_eq_empty, â† image_preimage_eq (_ âˆ© _) hf, preimage_inter, h.inter_eq,
    image_empty]
#align disjoint.of_preimage Disjoint.of_preimage

@[simp]
theorem disjoint_preimage_iff (hf : Surjective f) {s t : Set Î²} :
    Disjoint (f â»Â¹' s) (f â»Â¹' t) â†” Disjoint s t :=
  âŸ¨Disjoint.of_preimage hf, Disjoint.preimage _âŸ©
#align set.disjoint_preimage_iff Set.disjoint_preimage_iff

theorem preimage_eq_empty {s : Set Î²} (h : Disjoint s (range f)) :
    f â»Â¹' s = âˆ… := by
  simpa using h.preimage f
#align set.preimage_eq_empty Set.preimage_eq_empty

theorem preimage_eq_empty_iff {s : Set Î²} : f â»Â¹' s = âˆ… â†” Disjoint s (range f) :=
  âŸ¨fun h => by
    simp only [eq_empty_iff_forall_not_mem, disjoint_iff_inter_eq_empty, not_exists, mem_inter_iff,
      not_and, mem_range, mem_preimage] at h âŠ¢
    intro y hy x hx
    rw [â† hx] at hy
    exact h x hy,
  preimage_eq_emptyâŸ©
#align set.preimage_eq_empty_iff Set.preimage_eq_empty_iff

end Set

end Disjoint

section Sigma

variable {Î± : Type*} {Î² : Î± â†’ Type*} {i j : Î±} {s : Set (Î² i)}

lemma sigma_mk_preimage_image' (h : i â‰  j) : Sigma.mk j â»Â¹' (Sigma.mk i '' s) = âˆ… := by
  simp [image, h]

lemma sigma_mk_preimage_image_eq_self : Sigma.mk i â»Â¹' (Sigma.mk i '' s) = s := by
  simp [image]

end Sigma
