/-
Copyright (c) 2021 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.Algebra.BigOperators.Basic
import Mathlib.Data.Set.Pointwise.Basic

#align_import data.set.pointwise.big_operators from "leanprover-community/mathlib"@"fa2cb8a9e2b987db233e4e6eb47645feafba8861"

/-!
# Results about pointwise operations on sets and big operators.
-/


namespace Set

open BigOperators

open Pointwise Function

variable {Î¹ Î± Î² F : Type*}

section Monoid

variable [Monoid Î±] [Monoid Î²] [MonoidHomClass F Î± Î²]

@[to_additive]
theorem image_list_prod (f : F) :
    âˆ€ l : List (Set Î±), (f : Î± â†’ Î²) '' l.prod = (l.map fun s => f '' s).prod
  | [] => image_one.trans <| congr_arg singleton (map_one f)
  | a :: as => by rw [List.map_cons, List.prod_cons, List.prod_cons, image_mul, image_list_prod _ _]
                  -- ğŸ‰ no goals
#align set.image_list_prod Set.image_list_prod
#align set.image_list_sum Set.image_list_sum

end Monoid

section CommMonoid

variable [CommMonoid Î±] [CommMonoid Î²] [MonoidHomClass F Î± Î²]

@[to_additive]
theorem image_multiset_prod (f : F) :
    âˆ€ m : Multiset (Set Î±), (f : Î± â†’ Î²) '' m.prod = (m.map fun s => f '' s).prod :=
  Quotient.ind <| by
    simpa only [Multiset.quot_mk_to_coe, Multiset.coe_prod, Multiset.coe_map] using
      image_list_prod f
#align set.image_multiset_prod Set.image_multiset_prod
#align set.image_multiset_sum Set.image_multiset_sum

@[to_additive]
theorem image_finset_prod (f : F) (m : Finset Î¹) (s : Î¹ â†’ Set Î±) :
    ((f : Î± â†’ Î²) '' âˆ i in m, s i) = âˆ i in m, f '' s i :=
  (image_multiset_prod f _).trans <| congr_arg Multiset.prod <| Multiset.map_map _ _ _
#align set.image_finset_prod Set.image_finset_prod
#align set.image_finset_sum Set.image_finset_sum

/-- The n-ary version of `Set.mem_mul`. -/
@[to_additive " The n-ary version of `Set.mem_add`. "]
theorem mem_finset_prod (t : Finset Î¹) (f : Î¹ â†’ Set Î±) (a : Î±) :
    (a âˆˆ âˆ i in t, f i) â†” âˆƒ (g : Î¹ â†’ Î±) (_ : âˆ€ {i}, i âˆˆ t â†’ g i âˆˆ f i), âˆ i in t, g i = a := by
  classical
    induction' t using Finset.induction_on with i is hi ih generalizing a
    Â· simp_rw [Finset.prod_empty, Set.mem_one]
      exact âŸ¨fun h â†¦ âŸ¨fun _ â†¦ a, fun hi â†¦ False.elim (Finset.not_mem_empty _ hi), h.symmâŸ©,
        fun âŸ¨_, _, hfâŸ© â†¦ hf.symmâŸ©
    rw [Finset.prod_insert hi, Set.mem_mul]
    simp_rw [Finset.prod_insert hi]
    simp_rw [ih]
    constructor
    Â· rintro âŸ¨x, y, hx, âŸ¨g, hg, rflâŸ©, rflâŸ©
      refine âŸ¨Function.update g i x, ?_, ?_âŸ©
      Â· intro j hj
        obtain rfl | hj := Finset.mem_insert.mp hj
        Â· rwa [Function.update_same]
        Â· rw [update_noteq (ne_of_mem_of_not_mem hj hi)]
          exact hg hj
      Â· rw [Finset.prod_update_of_not_mem hi, Function.update_same]
    Â· rintro âŸ¨g, hg, rflâŸ©
      exact âŸ¨g i, is.prod g, hg (is.mem_insert_self _),
        âŸ¨âŸ¨g, fun hi â†¦ hg (Finset.mem_insert_of_mem hi), rflâŸ©, rflâŸ©âŸ©
#align set.mem_finset_prod Set.mem_finset_prod
#align set.mem_finset_sum Set.mem_finset_sum

/-- A version of `Set.mem_finset_prod` with a simpler RHS for products over a Fintype. -/
@[to_additive " A version of `Set.mem_finset_sum` with a simpler RHS for sums over a Fintype. "]
theorem mem_fintype_prod [Fintype Î¹] (f : Î¹ â†’ Set Î±) (a : Î±) :
    (a âˆˆ âˆ i, f i) â†” âˆƒ (g : Î¹ â†’ Î±) (_ : âˆ€ i, g i âˆˆ f i), âˆ i, g i = a := by
  rw [mem_finset_prod]
  -- âŠ¢ (âˆƒ g x, âˆ i : Î¹, g i = a) â†” âˆƒ g x, âˆ i : Î¹, g i = a
  simp
  -- ğŸ‰ no goals
#align set.mem_fintype_prod Set.mem_fintype_prod
#align set.mem_fintype_sum Set.mem_fintype_sum

/-- An n-ary version of `Set.mul_mem_mul`. -/
@[to_additive " An n-ary version of `Set.add_mem_add`. "]
theorem list_prod_mem_list_prod (t : List Î¹) (f : Î¹ â†’ Set Î±) (g : Î¹ â†’ Î±) (hg : âˆ€ i âˆˆ t, g i âˆˆ f i) :
    (t.map g).prod âˆˆ (t.map f).prod := by
  induction' t with h tl ih
  -- âŠ¢ List.prod (List.map g []) âˆˆ List.prod (List.map f [])
  Â· simp_rw [List.map_nil, List.prod_nil, Set.mem_one]
    -- ğŸ‰ no goals
  Â· simp_rw [List.map_cons, List.prod_cons]
    -- âŠ¢ g h * List.prod (List.map g tl) âˆˆ f h * List.prod (List.map f tl)
    exact mul_mem_mul (hg h <| List.mem_cons_self _ _)
      (ih fun i hi â†¦ hg i <| List.mem_cons_of_mem _ hi)
#align set.list_prod_mem_list_prod Set.list_prod_mem_list_prod
#align set.list_sum_mem_list_sum Set.list_sum_mem_list_sum

/-- An n-ary version of `Set.mul_subset_mul`. -/
@[to_additive " An n-ary version of `Set.add_subset_add`. "]
theorem list_prod_subset_list_prod (t : List Î¹) (fâ‚ fâ‚‚ : Î¹ â†’ Set Î±) (hf : âˆ€ i âˆˆ t, fâ‚ i âŠ† fâ‚‚ i) :
    (t.map fâ‚).prod âŠ† (t.map fâ‚‚).prod := by
  induction' t with h tl ih
  -- âŠ¢ List.prod (List.map fâ‚ []) âŠ† List.prod (List.map fâ‚‚ [])
  Â· rfl
    -- ğŸ‰ no goals
  Â· simp_rw [List.map_cons, List.prod_cons]
    -- âŠ¢ fâ‚ h * List.prod (List.map fâ‚ tl) âŠ† fâ‚‚ h * List.prod (List.map fâ‚‚ tl)
    exact mul_subset_mul (hf h <| List.mem_cons_self _ _)
      (ih fun i hi â†¦ hf i <| List.mem_cons_of_mem _ hi)
#align set.list_prod_subset_list_prod Set.list_prod_subset_list_prod
#align set.list_sum_subset_list_sum Set.list_sum_subset_list_sum

@[to_additive]
theorem list_prod_singleton {M : Type*} [CommMonoid M] (s : List M) :
    (s.map fun i â†¦ ({i} : Set M)).prod = {s.prod} :=
  (map_list_prod (singletonMonoidHom : M â†’* Set M) _).symm
#align set.list_prod_singleton Set.list_prod_singleton
#align set.list_sum_singleton Set.list_sum_singleton

/-- An n-ary version of `Set.mul_mem_mul`. -/
@[to_additive " An n-ary version of `Set.add_mem_add`. "]
theorem multiset_prod_mem_multiset_prod (t : Multiset Î¹) (f : Î¹ â†’ Set Î±) (g : Î¹ â†’ Î±)
    (hg : âˆ€ i âˆˆ t, g i âˆˆ f i) : (t.map g).prod âˆˆ (t.map f).prod := by
  induction t using Quotient.inductionOn
  -- âŠ¢ Multiset.prod (Multiset.map g (Quotient.mk (List.isSetoid Î¹) aâœ)) âˆˆ Multiset â€¦
  simp_rw [Multiset.quot_mk_to_coe, Multiset.coe_map, Multiset.coe_prod]
  -- âŠ¢ List.prod (List.map g aâœ) âˆˆ List.prod (List.map f aâœ)
  exact list_prod_mem_list_prod _ _ _ hg
  -- ğŸ‰ no goals
#align set.multiset_prod_mem_multiset_prod Set.multiset_prod_mem_multiset_prod
#align set.multiset_sum_mem_multiset_sum Set.multiset_sum_mem_multiset_sum

/-- An n-ary version of `Set.mul_subset_mul`. -/
@[to_additive " An n-ary version of `Set.add_subset_add`. "]
theorem multiset_prod_subset_multiset_prod (t : Multiset Î¹) (fâ‚ fâ‚‚ : Î¹ â†’ Set Î±)
    (hf : âˆ€ i âˆˆ t, fâ‚ i âŠ† fâ‚‚ i) : (t.map fâ‚).prod âŠ† (t.map fâ‚‚).prod := by
  induction t using Quotient.inductionOn
  -- âŠ¢ Multiset.prod (Multiset.map fâ‚ (Quotient.mk (List.isSetoid Î¹) aâœ)) âŠ† Multise â€¦
  simp_rw [Multiset.quot_mk_to_coe, Multiset.coe_map, Multiset.coe_prod]
  -- âŠ¢ List.prod (List.map fâ‚ aâœ) âŠ† List.prod (List.map fâ‚‚ aâœ)
  exact list_prod_subset_list_prod _ _ _ hf
  -- ğŸ‰ no goals
#align set.multiset_prod_subset_multiset_prod Set.multiset_prod_subset_multiset_prod
#align set.multiset_sum_subset_multiset_sum Set.multiset_sum_subset_multiset_sum

@[to_additive]
theorem multiset_prod_singleton {M : Type*} [CommMonoid M] (s : Multiset M) :
    (s.map fun i â†¦ ({i} : Set M)).prod = {s.prod} :=
  (map_multiset_prod (singletonMonoidHom : M â†’* Set M) _).symm
#align set.multiset_prod_singleton Set.multiset_prod_singleton
#align set.multiset_sum_singleton Set.multiset_sum_singleton

/-- An n-ary version of `Set.mul_mem_mul`. -/
@[to_additive " An n-ary version of `Set.add_mem_add`. "]
theorem finset_prod_mem_finset_prod (t : Finset Î¹) (f : Î¹ â†’ Set Î±) (g : Î¹ â†’ Î±)
    (hg : âˆ€ i âˆˆ t, g i âˆˆ f i) : (âˆ i in t, g i) âˆˆ âˆ i in t, f i :=
  multiset_prod_mem_multiset_prod _ _ _ hg
#align set.finset_prod_mem_finset_prod Set.finset_prod_mem_finset_prod
#align set.finset_sum_mem_finset_sum Set.finset_sum_mem_finset_sum

/-- An n-ary version of `Set.mul_subset_mul`. -/
@[to_additive " An n-ary version of `Set.add_subset_add`. "]
theorem finset_prod_subset_finset_prod (t : Finset Î¹) (fâ‚ fâ‚‚ : Î¹ â†’ Set Î±)
    (hf : âˆ€ i âˆˆ t, fâ‚ i âŠ† fâ‚‚ i) : âˆ i in t, fâ‚ i âŠ† âˆ i in t, fâ‚‚ i :=
  multiset_prod_subset_multiset_prod _ _ _ hf
#align set.finset_prod_subset_finset_prod Set.finset_prod_subset_finset_prod
#align set.finset_sum_subset_finset_sum Set.finset_sum_subset_finset_sum

@[to_additive]
theorem finset_prod_singleton {M Î¹ : Type*} [CommMonoid M] (s : Finset Î¹) (I : Î¹ â†’ M) :
    (âˆ i : Î¹ in s, ({I i} : Set M)) = {âˆ i : Î¹ in s, I i} :=
  (map_prod (singletonMonoidHom : M â†’* Set M) _ _).symm
#align set.finset_prod_singleton Set.finset_prod_singleton
#align set.finset_sum_singleton Set.finset_sum_singleton

/-- The n-ary version of `Set.image_mul_prod`. -/
@[to_additive "The n-ary version of `Set.add_image_prod`. "]
theorem image_finset_prod_pi (l : Finset Î¹) (S : Î¹ â†’ Set Î±) :
    (fun f : Î¹ â†’ Î± => âˆ i in l, f i) '' (l : Set Î¹).pi S = âˆ i in l, S i := by
  ext
  -- âŠ¢ xâœ âˆˆ (fun f => âˆ i in l, f i) '' pi (â†‘l) S â†” xâœ âˆˆ âˆ i in l, S i
  simp_rw [mem_finset_prod, mem_image, mem_pi, exists_prop, Finset.mem_coe]
  -- ğŸ‰ no goals
#align set.image_finset_prod_pi Set.image_finset_prod_pi
#align set.image_finset_sum_pi Set.image_finset_sum_pi

/-- A special case of `Set.image_finset_prod_pi` for `Finset.univ`. -/
@[to_additive "A special case of `Set.image_finset_sum_pi` for `Finset.univ`. "]
theorem image_fintype_prod_pi [Fintype Î¹] (S : Î¹ â†’ Set Î±) :
    (fun f : Î¹ â†’ Î± => âˆ i, f i) '' univ.pi S = âˆ i, S i := by
  simpa only [Finset.coe_univ] using image_finset_prod_pi Finset.univ S
  -- ğŸ‰ no goals
#align set.image_fintype_prod_pi Set.image_fintype_prod_pi
#align set.image_fintype_sum_pi Set.image_fintype_sum_pi

end CommMonoid

/-! TODO: define `decidable_mem_finset_prod` and `decidable_mem_finset_sum`. -/


end Set
