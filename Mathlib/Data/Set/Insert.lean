/-
Copyright (c) 2014 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Leonardo de Moura
-/
import Mathlib.Data.Set.Disjoint

/-!
# Lemmas about insertion, singleton, and pairs

This file provides extra lemmas about `insert`, `singleton`, and `pair`.

## Tags

insert, singleton

-/

assert_not_exists HeytingAlgebra

/-! ### Set coercion to a type -/

open Function

universe u v

namespace Set

variable {Î± : Type u} {s t : Set Î±} {a b : Î±}

/-!
### Lemmas about `insert`

`insert a s` is the set `{a} âˆª s`.
-/

theorem insert_def (x : Î±) (s : Set Î±) : insert x s = { y | y = x âˆ¨ y âˆˆ s } :=
  rfl

@[simp]
theorem subset_insert (x : Î±) (s : Set Î±) : s âŠ† insert x s := fun _ => Or.inr

theorem mem_insert (x : Î±) (s : Set Î±) : x âˆˆ insert x s :=
  Or.inl rfl

theorem mem_insert_of_mem {x : Î±} {s : Set Î±} (y : Î±) : x âˆˆ s â†’ x âˆˆ insert y s :=
  Or.inr

theorem eq_or_mem_of_mem_insert {x a : Î±} {s : Set Î±} : x âˆˆ insert a s â†’ x = a âˆ¨ x âˆˆ s :=
  id

theorem mem_of_mem_insert_of_ne : b âˆˆ insert a s â†’ b â‰  a â†’ b âˆˆ s :=
  Or.resolve_left

theorem eq_of_mem_insert_of_notMem : b âˆˆ insert a s â†’ b âˆ‰ s â†’ b = a :=
  Or.resolve_right

@[deprecated (since := "2025-05-23")]
alias eq_of_not_mem_of_mem_insert := eq_of_mem_insert_of_notMem

@[simp]
theorem mem_insert_iff {x a : Î±} {s : Set Î±} : x âˆˆ insert a s â†” x = a âˆ¨ x âˆˆ s :=
  Iff.rfl

@[simp]
theorem insert_eq_of_mem {a : Î±} {s : Set Î±} (h : a âˆˆ s) : insert a s = s :=
  ext fun _ => or_iff_right_of_imp fun e => e.symm â–¸ h

theorem ne_insert_of_notMem {s : Set Î±} (t : Set Î±) {a : Î±} : a âˆ‰ s â†’ s â‰  insert a t :=
  mt fun e => e.symm â–¸ mem_insert _ _

@[deprecated (since := "2025-05-23")] alias ne_insert_of_not_mem := ne_insert_of_notMem

@[simp]
theorem insert_eq_self : insert a s = s â†” a âˆˆ s :=
  âŸ¨fun h => h â–¸ mem_insert _ _, insert_eq_of_memâŸ©

theorem insert_ne_self : insert a s â‰  s â†” a âˆ‰ s :=
  insert_eq_self.not

theorem insert_subset_iff : insert a s âŠ† t â†” a âˆˆ t âˆ§ s âŠ† t := by
  simp only [subset_def, mem_insert_iff, or_imp, forall_and, forall_eq]

theorem insert_subset (ha : a âˆˆ t) (hs : s âŠ† t) : insert a s âŠ† t :=
  insert_subset_iff.mpr âŸ¨ha, hsâŸ©

@[gcongr]
theorem insert_subset_insert (h : s âŠ† t) : insert a s âŠ† insert a t := fun _ => Or.imp_right (@h _)

@[simp] theorem insert_subset_insert_iff (ha : a âˆ‰ s) : insert a s âŠ† insert a t â†” s âŠ† t := by
  refine âŸ¨fun h x hx => ?_, insert_subset_insertâŸ©
  rcases h (subset_insert _ _ hx) with (rfl | hxt)
  exacts [(ha hx).elim, hxt]

theorem subset_insert_iff_of_notMem (ha : a âˆ‰ s) : s âŠ† insert a t â†” s âŠ† t :=
  forallâ‚‚_congr fun _ hb => or_iff_right <| ne_of_mem_of_not_mem hb ha

@[deprecated (since := "2025-05-23")]
alias subset_insert_iff_of_not_mem := subset_insert_iff_of_notMem

theorem ssubset_iff_insert {s t : Set Î±} : s âŠ‚ t â†” âˆƒ a âˆ‰ s, insert a s âŠ† t := by
  simp only [insert_subset_iff, ssubset_def, not_subset]
  aesop

theorem _root_.HasSubset.Subset.ssubset_of_mem_notMem (hst : s âŠ† t) (hat : a âˆˆ t) (has : a âˆ‰ s) :
    s âŠ‚ t := hst.ssubset_of_not_subset fun a â†¦ has (a hat)

@[deprecated (since := "2025-05-23")]
alias _root_.HasSubset.Subset.ssubset_of_mem_not_mem :=
  _root_.HasSubset.Subset.ssubset_of_mem_notMem

theorem ssubset_insert {s : Set Î±} {a : Î±} (h : a âˆ‰ s) : s âŠ‚ insert a s :=
  ssubset_iff_insert.2 âŸ¨a, h, Subset.rflâŸ©

theorem insert_comm (a b : Î±) (s : Set Î±) : insert a (insert b s) = insert b (insert a s) :=
  ext fun _ => or_left_comm

theorem insert_idem (a : Î±) (s : Set Î±) : insert a (insert a s) = insert a s :=
  insert_eq_of_mem <| mem_insert _ _

theorem insert_union : insert a s âˆª t = insert a (s âˆª t) :=
  ext fun _ => or_assoc

@[simp]
theorem union_insert : s âˆª insert a t = insert a (s âˆª t) :=
  ext fun _ => or_left_comm

@[simp]
theorem insert_nonempty (a : Î±) (s : Set Î±) : (insert a s).Nonempty :=
  âŸ¨a, mem_insert a sâŸ©

instance (a : Î±) (s : Set Î±) : Nonempty (insert a s : Set Î±) :=
  (insert_nonempty a s).to_subtype

theorem insert_inter_distrib (a : Î±) (s t : Set Î±) : insert a (s âˆ© t) = insert a s âˆ© insert a t :=
  ext fun _ => or_and_left

theorem insert_union_distrib (a : Î±) (s t : Set Î±) : insert a (s âˆª t) = insert a s âˆª insert a t :=
  ext fun _ => or_or_distrib_left

-- useful in proofs by induction
theorem forall_of_forall_insert {P : Î± â†’ Prop} {a : Î±} {s : Set Î±} (H : âˆ€ x, x âˆˆ insert a s â†’ P x)
    (x) (h : x âˆˆ s) : P x :=
  H _ (Or.inr h)

theorem forall_insert_of_forall {P : Î± â†’ Prop} {a : Î±} {s : Set Î±} (H : âˆ€ x, x âˆˆ s â†’ P x) (ha : P a)
    (x) (h : x âˆˆ insert a s) : P x :=
  h.elim (fun e => e.symm â–¸ ha) (H _)

theorem exists_mem_insert {P : Î± â†’ Prop} {a : Î±} {s : Set Î±} :
    (âˆƒ x âˆˆ insert a s, P x) â†” (P a âˆ¨ âˆƒ x âˆˆ s, P x) := by
  simp [mem_insert_iff, or_and_right, exists_or]

theorem forall_mem_insert {P : Î± â†’ Prop} {a : Î±} {s : Set Î±} :
    (âˆ€ x âˆˆ insert a s, P x) â†” P a âˆ§ âˆ€ x âˆˆ s, P x :=
  forallâ‚‚_or_left.trans <| and_congr_left' forall_eq

/-- Inserting an element to a set is equivalent to the option type. -/
def subtypeInsertEquivOption
    [DecidableEq Î±] {t : Set Î±} {x : Î±} (h : x âˆ‰ t) :
    { i // i âˆˆ insert x t } â‰ƒ Option { i // i âˆˆ t } where
  toFun y := if h : â†‘y = x then none else some âŸ¨y, (mem_insert_iff.mp y.2).resolve_left hâŸ©
  invFun y := (y.elim âŸ¨x, mem_insert _ _âŸ©) fun z => âŸ¨z, mem_insert_of_mem _ z.2âŸ©
  left_inv y := by
    by_cases h : â†‘y = x
    Â· simp only [Subtype.ext_iff, h, Option.elim, dif_pos]
    Â· simp only [h, Option.elim, dif_neg, not_false_iff, Subtype.coe_eta]
  right_inv := by
    rintro (_ | y)
    Â· simp only [Option.elim, dif_pos]
    Â· have : â†‘y â‰  x := by
        rintro âŸ¨âŸ©
        exact h y.2
      simp only [this, Option.elim, Subtype.eta, dif_neg, not_false_iff, Subtype.coe_mk]

/-! ### Lemmas about singletons -/

instance : LawfulSingleton Î± (Set Î±) :=
  âŸ¨fun x => Set.ext fun a => by
    simp only [mem_empty_iff_false, mem_insert_iff, or_false]
    exact Iff.rflâŸ©

theorem singleton_def (a : Î±) : ({a} : Set Î±) = insert a âˆ… :=
  (insert_empty_eq a).symm

@[simp]
theorem mem_singleton_iff {a b : Î±} : a âˆˆ ({b} : Set Î±) â†” a = b :=
  Iff.rfl

theorem notMem_singleton_iff {a b : Î±} : a âˆ‰ ({b} : Set Î±) â†” a â‰  b :=
  Iff.rfl

@[deprecated (since := "2025-05-23")] alias not_mem_singleton_iff := notMem_singleton_iff

@[simp]
theorem setOf_eq_eq_singleton {a : Î±} : { n | n = a } = {a} :=
  rfl

@[simp]
theorem setOf_eq_eq_singleton' {a : Î±} : { x | a = x } = {a} :=
  ext fun _ => eq_comm

-- TODO: again, annotation needed
-- Not `@[simp]` since `mem_singleton_iff` proves it.
theorem mem_singleton (a : Î±) : a âˆˆ ({a} : Set Î±) :=
  @rfl _ _

theorem eq_of_mem_singleton {x y : Î±} (h : x âˆˆ ({y} : Set Î±)) : x = y :=
  h

@[simp]
theorem singleton_eq_singleton_iff {x y : Î±} : {x} = ({y} : Set Î±) â†” x = y :=
  Set.ext_iff.trans eq_iff_eq_cancel_left

theorem singleton_injective : Injective (singleton : Î± â†’ Set Î±) := fun _ _ =>
  singleton_eq_singleton_iff.mp

theorem mem_singleton_of_eq {x y : Î±} (H : x = y) : x âˆˆ ({y} : Set Î±) :=
  H

theorem insert_eq (x : Î±) (s : Set Î±) : insert x s = ({x} : Set Î±) âˆª s :=
  rfl

@[simp]
theorem singleton_nonempty (a : Î±) : ({a} : Set Î±).Nonempty :=
  âŸ¨a, rflâŸ©

@[simp]
theorem singleton_ne_empty (a : Î±) : ({a} : Set Î±) â‰  âˆ… :=
  (singleton_nonempty _).ne_empty

theorem empty_ssubset_singleton : (âˆ… : Set Î±) âŠ‚ {a} :=
  (singleton_nonempty _).empty_ssubset

@[simp]
theorem singleton_subset_iff {a : Î±} {s : Set Î±} : {a} âŠ† s â†” a âˆˆ s :=
  forall_eq

theorem singleton_subset_singleton : ({a} : Set Î±) âŠ† {b} â†” a = b := by simp

@[gcongr] protected alias âŸ¨_, GCongr.singleton_subset_singletonâŸ© := singleton_subset_singleton

theorem set_compr_eq_eq_singleton {a : Î±} : { b | b = a } = {a} :=
  rfl

@[simp]
theorem singleton_union : {a} âˆª s = insert a s :=
  rfl

@[simp]
theorem union_singleton : s âˆª {a} = insert a s :=
  union_comm _ _

@[simp]
theorem singleton_inter_nonempty : ({a} âˆ© s).Nonempty â†” a âˆˆ s := by
  simp only [Set.Nonempty, mem_inter_iff, mem_singleton_iff, exists_eq_left]

@[simp]
theorem inter_singleton_nonempty : (s âˆ© {a}).Nonempty â†” a âˆˆ s := by
  rw [inter_comm, singleton_inter_nonempty]

@[simp]
theorem singleton_inter_eq_empty : {a} âˆ© s = âˆ… â†” a âˆ‰ s :=
  not_nonempty_iff_eq_empty.symm.trans singleton_inter_nonempty.not

@[simp]
theorem inter_singleton_eq_empty : s âˆ© {a} = âˆ… â†” a âˆ‰ s := by
  rw [inter_comm, singleton_inter_eq_empty]

theorem notMem_singleton_empty {s : Set Î±} : s âˆ‰ ({âˆ…} : Set (Set Î±)) â†” s.Nonempty :=
  nonempty_iff_ne_empty.symm

@[deprecated (since := "2025-05-24")] alias nmem_singleton_empty := notMem_singleton_empty

instance uniqueSingleton (a : Î±) : Unique (â†¥({a} : Set Î±)) :=
  âŸ¨âŸ¨âŸ¨a, mem_singleton aâŸ©âŸ©, fun âŸ¨_, hâŸ© => Subtype.eq hâŸ©

theorem eq_singleton_iff_unique_mem : s = {a} â†” a âˆˆ s âˆ§ âˆ€ x âˆˆ s, x = a :=
  Subset.antisymm_iff.trans <| and_comm.trans <| and_congr_left' singleton_subset_iff

theorem eq_singleton_iff_nonempty_unique_mem : s = {a} â†” s.Nonempty âˆ§ âˆ€ x âˆˆ s, x = a :=
  eq_singleton_iff_unique_mem.trans <|
    and_congr_left fun H => âŸ¨fun h' => âŸ¨_, h'âŸ©, fun âŸ¨x, hâŸ© => H x h â–¸ hâŸ©

theorem setOf_mem_list_eq_replicate {l : List Î±} {a : Î±} :
    { x | x âˆˆ l } = {a} â†” âˆƒ n > 0, l = List.replicate n a := by
  simpa +contextual [Set.ext_iff, iff_iff_implies_and_implies, forall_and, List.eq_replicate_iff,
    List.length_pos_iff_exists_mem] using âŸ¨fun _ _ â†¦ âŸ¨_, â€¹_â€ºâŸ©, fun x hx h â†¦ h _ hx â–¸ hxâŸ©

theorem setOf_mem_list_eq_singleton_of_nodup {l : List Î±} (H : l.Nodup) {a : Î±} :
    { x | x âˆˆ l } = {a} â†” l = [a] := by
  constructor
  Â· rw [setOf_mem_list_eq_replicate]
    rintro âŸ¨n, hn, rflâŸ©
    simp only [List.nodup_replicate] at H
    simp [show n = 1 by omega]
  Â· rintro rfl
    simp

-- while `simp` is capable of proving this, it is not capable of turning the LHS into the RHS.
@[simp]
theorem default_coe_singleton (x : Î±) : (default : ({x} : Set Î±)) = âŸ¨x, rflâŸ© :=
  rfl

@[simp]
theorem subset_singleton_iff {Î± : Type*} {s : Set Î±} {x : Î±} : s âŠ† {x} â†” âˆ€ y âˆˆ s, y = x :=
  Iff.rfl

theorem subset_singleton_iff_eq {s : Set Î±} {x : Î±} : s âŠ† {x} â†” s = âˆ… âˆ¨ s = {x} := by
  obtain rfl | hs := s.eq_empty_or_nonempty
  Â· exact âŸ¨fun _ => Or.inl rfl, fun _ => empty_subset _âŸ©
  Â· simp [eq_singleton_iff_nonempty_unique_mem, hs, hs.ne_empty]

theorem Nonempty.subset_singleton_iff (h : s.Nonempty) : s âŠ† {a} â†” s = {a} :=
  subset_singleton_iff_eq.trans <| or_iff_right h.ne_empty

theorem ssubset_singleton_iff {s : Set Î±} {x : Î±} : s âŠ‚ {x} â†” s = âˆ… := by
  rw [ssubset_iff_subset_ne, subset_singleton_iff_eq, or_and_right, and_not_self_iff, or_false,
    and_iff_left_iff_imp]
  exact fun h => h â–¸ (singleton_ne_empty _).symm

theorem eq_empty_of_ssubset_singleton {s : Set Î±} {x : Î±} (hs : s âŠ‚ {x}) : s = âˆ… :=
  ssubset_singleton_iff.1 hs

theorem eq_of_nonempty_of_subsingleton {Î±} [Subsingleton Î±] (s t : Set Î±) [Nonempty s]
    [Nonempty t] : s = t :=
  Nonempty.of_subtype.eq_univ.trans Nonempty.of_subtype.eq_univ.symm

theorem eq_of_nonempty_of_subsingleton' {Î±} [Subsingleton Î±] {s : Set Î±} (t : Set Î±)
    (hs : s.Nonempty) [Nonempty t] : s = t :=
  have := hs.to_subtype; eq_of_nonempty_of_subsingleton s t

theorem Nonempty.eq_zero [Subsingleton Î±] [Zero Î±] {s : Set Î±} (h : s.Nonempty) :
    s = {0} := eq_of_nonempty_of_subsingleton' {0} h

theorem Nonempty.eq_one [Subsingleton Î±] [One Î±] {s : Set Î±} (h : s.Nonempty) :
    s = {1} := eq_of_nonempty_of_subsingleton' {1} h

/-! ### Disjointness -/

@[simp default + 1]
lemma disjoint_singleton_left : Disjoint {a} s â†” a âˆ‰ s := by simp [Set.disjoint_iff, subset_def]

@[simp]
lemma disjoint_singleton_right : Disjoint s {a} â†” a âˆ‰ s :=
  disjoint_comm.trans disjoint_singleton_left

lemma disjoint_singleton : Disjoint ({a} : Set Î±) {b} â†” a â‰  b := by
  simp

@[simp]
theorem disjoint_insert_left : Disjoint (insert a s) t â†” a âˆ‰ t âˆ§ Disjoint s t := by
  simp only [Set.disjoint_left, Set.mem_insert_iff, forall_eq_or_imp]

@[simp]
theorem disjoint_insert_right : Disjoint s (insert a t) â†” a âˆ‰ s âˆ§ Disjoint s t := by
  rw [disjoint_comm, disjoint_insert_left, disjoint_comm]

theorem insert_inj (ha : a âˆ‰ s) : insert a s = insert b s â†” a = b :=
  âŸ¨fun h => eq_of_mem_insert_of_notMem (h â–¸ mem_insert a s) ha,
    congr_arg (fun x => insert x s)âŸ©

@[simp]
theorem insert_diff_eq_singleton {a : Î±} {s : Set Î±} (h : a âˆ‰ s) : insert a s \ s = {a} := by
  ext
  rw [Set.mem_diff, Set.mem_insert_iff, Set.mem_singleton_iff, or_and_right, and_not_self_iff,
    or_false, and_iff_left_iff_imp]
  rintro rfl
  exact h

theorem inter_insert_of_mem (h : a âˆˆ s) : s âˆ© insert a t = insert a (s âˆ© t) := by
  rw [insert_inter_distrib, insert_eq_of_mem h]

theorem insert_inter_of_mem (h : a âˆˆ t) : insert a s âˆ© t = insert a (s âˆ© t) := by
  rw [insert_inter_distrib, insert_eq_of_mem h]

theorem inter_insert_of_notMem (h : a âˆ‰ s) : s âˆ© insert a t = s âˆ© t :=
  ext fun _ => and_congr_right fun hx => or_iff_right <| ne_of_mem_of_not_mem hx h

@[deprecated (since := "2025-05-23")] alias inter_insert_of_not_mem := inter_insert_of_notMem

theorem insert_inter_of_notMem (h : a âˆ‰ t) : insert a s âˆ© t = s âˆ© t :=
  ext fun _ => and_congr_left fun hx => or_iff_right <| ne_of_mem_of_not_mem hx h

@[deprecated (since := "2025-05-23")] alias insert_inter_of_not_mem := insert_inter_of_notMem

/-! ### Lemmas about pairs -/

theorem pair_eq_singleton (a : Î±) : ({a, a} : Set Î±) = {a} :=
  union_self _

theorem pair_comm (a b : Î±) : ({a, b} : Set Î±) = {b, a} :=
  union_comm _ _

theorem pair_eq_pair_iff {x y z w : Î±} :
    ({x, y} : Set Î±) = {z, w} â†” x = z âˆ§ y = w âˆ¨ x = w âˆ§ y = z := by
  simp [subset_antisymm_iff, insert_subset_iff]; aesop

theorem pair_subset_iff : {a, b} âŠ† s â†” a âˆˆ s âˆ§ b âˆˆ s := by
  rw [insert_subset_iff, singleton_subset_iff]

theorem pair_subset (ha : a âˆˆ s) (hb : b âˆˆ s) : {a, b} âŠ† s :=
  pair_subset_iff.2 âŸ¨ha,hbâŸ©

theorem subset_pair_iff : s âŠ† {a, b} â†” âˆ€ x âˆˆ s, x = a âˆ¨ x = b := by
  simp [subset_def]

theorem subset_pair_iff_eq {x y : Î±} : s âŠ† {x, y} â†” s = âˆ… âˆ¨ s = {x} âˆ¨ s = {y} âˆ¨ s = {x, y} where
  mp := by simp [subset_def, Set.ext_iff]; grind
  mpr := by simp +contextual [or_imp]

theorem Nonempty.subset_pair_iff_eq (hs : s.Nonempty) :
    s âŠ† {a, b} â†” s = {a} âˆ¨ s = {b} âˆ¨ s = {a, b} := by
  rw [Set.subset_pair_iff_eq, or_iff_right]; exact hs.ne_empty

/-! ### Powerset -/

/-- The powerset of a singleton contains only `âˆ…` and the singleton itself. -/
theorem powerset_singleton (x : Î±) : ð’«({x} : Set Î±) = {âˆ…, {x}} := by
  ext y
  rw [mem_powerset_iff, subset_singleton_iff_eq, mem_insert_iff, mem_singleton_iff]

section
variable {Î± Î² : Type*} {a : Î±} {b : Î²}

lemma preimage_fst_singleton_eq_range : (Prod.fst â»Â¹' {a} : Set (Î± Ã— Î²)) = range (a, Â·) := by
  aesop

lemma preimage_snd_singleton_eq_range : (Prod.snd â»Â¹' {b} : Set (Î± Ã— Î²)) = range (Â·, b) := by
  aesop

end

/-! ### Lemmas about `inclusion`, the injection of subtypes induced by `âŠ†` -/

/-! ### Decidability instances for sets -/

variable {Î± : Type u} (s t : Set Î±) (a b : Î±)

instance decidableSingleton [Decidable (a = b)] : Decidable (a âˆˆ ({b} : Set Î±)) :=
  inferInstanceAs (Decidable (a = b))

end Set

open Set

@[simp] theorem Prop.compl_singleton (p : Prop) : ({p}á¶œ : Set Prop) = {Â¬p} :=
  ext fun q â†¦ by simpa [@Iff.comm q] using not_iff
