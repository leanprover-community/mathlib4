/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Kyle Miller
-/
import Mathlib.Data.Finset.Basic
import Mathlib.Data.Finite.Basic
import Mathlib.Data.Set.Functor
import Mathlib.Data.Set.Lattice

#align_import data.set.finite from "leanprover-community/mathlib"@"65a1391a0106c9204fe45bc73a039f056558cb83"

/-!
# Finite sets

This file defines predicates for finite and infinite sets and provides
`Fintype` instances for many set constructions. It also proves basic facts
about finite sets and gives ways to manipulate `Set.Finite` expressions.

## Main definitions

* `Set.Finite : Set Î± â†’ Prop`
* `Set.Infinite : Set Î± â†’ Prop`
* `Set.toFinite` to prove `Set.Finite` for a `Set` from a `Finite` instance.
* `Set.Finite.toFinset` to noncomputably produce a `Finset` from a `Set.Finite` proof.
  (See `Set.toFinset` for a computable version.)

## Implementation

A finite set is defined to be a set whose coercion to a type has a `Finite` instance.

There are two components to finiteness constructions. The first is `Fintype` instances for each
construction. This gives a way to actually compute a `Finset` that represents the set, and these
may be accessed using `set.toFinset`. This gets the `Finset` in the correct form, since otherwise
`Finset.univ : Finset s` is a `Finset` for the subtype for `s`. The second component is
"constructors" for `Set.Finite` that give proofs that `Fintype` instances exist classically given
other `Set.Finite` proofs. Unlike the `Fintype` instances, these *do not* use any decidability
instances since they do not compute anything.

## Tags

finite sets
-/

assert_not_exists OrderedRing
assert_not_exists MonoidWithZero

open Set Function

universe u v w x

variable {Î± : Type u} {Î² : Type v} {Î¹ : Sort w} {Î³ : Type x}

namespace Set

/-- A set is finite if the corresponding `Subtype` is finite,
i.e., if there exists a natural `n : â„•` and an equivalence `s â‰ƒ Fin n`. -/
protected def Finite (s : Set Î±) : Prop := Finite s
#align set.finite Set.Finite

-- The `protected` attribute does not take effect within the same namespace block.
end Set

namespace Set

theorem finite_def {s : Set Î±} : s.Finite â†” Nonempty (Fintype s) :=
  finite_iff_nonempty_fintype s
#align set.finite_def Set.finite_def

protected alias âŸ¨Finite.nonempty_fintype, _âŸ© := finite_def
#align set.finite.nonempty_fintype Set.Finite.nonempty_fintype

theorem finite_coe_iff {s : Set Î±} : Finite s â†” s.Finite := .rfl
#align set.finite_coe_iff Set.finite_coe_iff

/-- Constructor for `Set.Finite` using a `Finite` instance. -/
theorem toFinite (s : Set Î±) [Finite s] : s.Finite := â€¹_â€º
#align set.to_finite Set.toFinite

/-- Construct a `Finite` instance for a `Set` from a `Finset` with the same elements. -/
protected theorem Finite.ofFinset {p : Set Î±} (s : Finset Î±) (H : âˆ€ x, x âˆˆ s â†” x âˆˆ p) : p.Finite :=
  have := Fintype.ofFinset s H; p.toFinite
#align set.finite.of_finset Set.Finite.ofFinset

/-- Projection of `Set.Finite` to its `Finite` instance.
This is intended to be used with dot notation.
See also `Set.Finite.Fintype` and `Set.Finite.nonempty_fintype`. -/
protected theorem Finite.to_subtype {s : Set Î±} (h : s.Finite) : Finite s := h
#align set.finite.to_subtype Set.Finite.to_subtype

/-- A finite set coerced to a type is a `Fintype`.
This is the `Fintype` projection for a `Set.Finite`.

Note that because `Finite` isn't a typeclass, this definition will not fire if it
is made into an instance -/
protected noncomputable def Finite.fintype {s : Set Î±} (h : s.Finite) : Fintype s :=
  h.nonempty_fintype.some
#align set.finite.fintype Set.Finite.fintype

/-- Using choice, get the `Finset` that represents this `Set`. -/
protected noncomputable def Finite.toFinset {s : Set Î±} (h : s.Finite) : Finset Î± :=
  @Set.toFinset _ _ h.fintype
#align set.finite.to_finset Set.Finite.toFinset

theorem Finite.toFinset_eq_toFinset {s : Set Î±} [Fintype s] (h : s.Finite) :
    h.toFinset = s.toFinset := by
  -- Porting note: was `rw [Finite.toFinset]; congr`
  -- in Lean 4, a goal is left after `congr`
  have : h.fintype = â€¹_â€º := Subsingleton.elim _ _
  rw [Finite.toFinset, this]
#align set.finite.to_finset_eq_to_finset Set.Finite.toFinset_eq_toFinset

@[simp]
theorem toFinite_toFinset (s : Set Î±) [Fintype s] : s.toFinite.toFinset = s.toFinset :=
  s.toFinite.toFinset_eq_toFinset
#align set.to_finite_to_finset Set.toFinite_toFinset

theorem Finite.exists_finset {s : Set Î±} (h : s.Finite) :
    âˆƒ s' : Finset Î±, âˆ€ a : Î±, a âˆˆ s' â†” a âˆˆ s := by
  cases h.nonempty_fintype
  exact âŸ¨s.toFinset, fun _ => mem_toFinsetâŸ©
#align set.finite.exists_finset Set.Finite.exists_finset

theorem Finite.exists_finset_coe {s : Set Î±} (h : s.Finite) : âˆƒ s' : Finset Î±, â†‘s' = s := by
  cases h.nonempty_fintype
  exact âŸ¨s.toFinset, s.coe_toFinsetâŸ©
#align set.finite.exists_finset_coe Set.Finite.exists_finset_coe

/-- Finite sets can be lifted to finsets. -/
instance : CanLift (Set Î±) (Finset Î±) (â†‘) Set.Finite where prf _ hs := hs.exists_finset_coe

/-- A set is infinite if it is not finite.

This is protected so that it does not conflict with global `Infinite`. -/
protected def Infinite (s : Set Î±) : Prop :=
  Â¬s.Finite
#align set.infinite Set.Infinite

@[simp]
theorem not_infinite {s : Set Î±} : Â¬s.Infinite â†” s.Finite :=
  not_not
#align set.not_infinite Set.not_infinite

alias âŸ¨_, Finite.not_infiniteâŸ© := not_infinite
#align set.finite.not_infinite Set.Finite.not_infinite

attribute [simp] Finite.not_infinite

/-- See also `finite_or_infinite`, `fintypeOrInfinite`. -/
protected theorem finite_or_infinite (s : Set Î±) : s.Finite âˆ¨ s.Infinite :=
  em _
#align set.finite_or_infinite Set.finite_or_infinite

protected theorem infinite_or_finite (s : Set Î±) : s.Infinite âˆ¨ s.Finite :=
  em' _
#align set.infinite_or_finite Set.infinite_or_finite

/-! ### Basic properties of `Set.Finite.toFinset` -/


namespace Finite

variable {s t : Set Î±} {a : Î±} (hs : s.Finite) {ht : t.Finite}

@[simp]
protected theorem mem_toFinset : a âˆˆ hs.toFinset â†” a âˆˆ s :=
  @mem_toFinset _ _ hs.fintype _
#align set.finite.mem_to_finset Set.Finite.mem_toFinset

@[simp]
protected theorem coe_toFinset : (hs.toFinset : Set Î±) = s :=
  @coe_toFinset _ _ hs.fintype
#align set.finite.coe_to_finset Set.Finite.coe_toFinset

@[simp]
protected theorem toFinset_nonempty : hs.toFinset.Nonempty â†” s.Nonempty := by
  rw [â† Finset.coe_nonempty, Finite.coe_toFinset]
#align set.finite.to_finset_nonempty Set.Finite.toFinset_nonempty

/-- Note that this is an equality of types not holding definitionally. Use wisely. -/
theorem coeSort_toFinset : â†¥hs.toFinset = â†¥s := by
  rw [â† Finset.coe_sort_coe _, hs.coe_toFinset]
#align set.finite.coe_sort_to_finset Set.Finite.coeSort_toFinset

/-- The identity map, bundled as an equivalence between the subtypes of `s : Set Î±` and of
`h.toFinset : Finset Î±`, where `h` is a proof of finiteness of `s`. -/
@[simps!] def subtypeEquivToFinset : {x // x âˆˆ s} â‰ƒ {x // x âˆˆ hs.toFinset} :=
  (Equiv.refl Î±).subtypeEquiv fun _ â†¦ hs.mem_toFinset.symm

variable {hs}

@[simp]
protected theorem toFinset_inj : hs.toFinset = ht.toFinset â†” s = t :=
  @toFinset_inj _ _ _ hs.fintype ht.fintype
#align set.finite.to_finset_inj Set.Finite.toFinset_inj

@[simp]
theorem toFinset_subset {t : Finset Î±} : hs.toFinset âŠ† t â†” s âŠ† t := by
  rw [â† Finset.coe_subset, Finite.coe_toFinset]
#align set.finite.to_finset_subset Set.Finite.toFinset_subset

@[simp]
theorem toFinset_ssubset {t : Finset Î±} : hs.toFinset âŠ‚ t â†” s âŠ‚ t := by
  rw [â† Finset.coe_ssubset, Finite.coe_toFinset]
#align set.finite.to_finset_ssubset Set.Finite.toFinset_ssubset

@[simp]
theorem subset_toFinset {s : Finset Î±} : s âŠ† ht.toFinset â†” â†‘s âŠ† t := by
  rw [â† Finset.coe_subset, Finite.coe_toFinset]
#align set.finite.subset_to_finset Set.Finite.subset_toFinset

@[simp]
theorem ssubset_toFinset {s : Finset Î±} : s âŠ‚ ht.toFinset â†” â†‘s âŠ‚ t := by
  rw [â† Finset.coe_ssubset, Finite.coe_toFinset]
#align set.finite.ssubset_to_finset Set.Finite.ssubset_toFinset

@[mono]
protected theorem toFinset_subset_toFinset : hs.toFinset âŠ† ht.toFinset â†” s âŠ† t := by
  simp only [â† Finset.coe_subset, Finite.coe_toFinset]
#align set.finite.to_finset_subset_to_finset Set.Finite.toFinset_subset_toFinset

@[mono]
protected theorem toFinset_ssubset_toFinset : hs.toFinset âŠ‚ ht.toFinset â†” s âŠ‚ t := by
  simp only [â† Finset.coe_ssubset, Finite.coe_toFinset]
#align set.finite.to_finset_ssubset_to_finset Set.Finite.toFinset_ssubset_toFinset

alias âŸ¨_, toFinset_monoâŸ© := Finite.toFinset_subset_toFinset
#align set.finite.to_finset_mono Set.Finite.toFinset_mono

alias âŸ¨_, toFinset_strictMonoâŸ© := Finite.toFinset_ssubset_toFinset
#align set.finite.to_finset_strict_mono Set.Finite.toFinset_strictMono

-- Porting note: attribute [protected] doesn't work
-- attribute [protected] toFinset_mono toFinset_strictMono

-- Porting note: `simp` can simplify LHS but then it simplifies something
-- in the generated `Fintype {x | p x}` instance and fails to apply `Set.toFinset_setOf`
@[simp high]
protected theorem toFinset_setOf [Fintype Î±] (p : Î± â†’ Prop) [DecidablePred p]
    (h : { x | p x }.Finite) : h.toFinset = Finset.univ.filter p := by
  ext
  -- Porting note: `simp` doesn't use the `simp` lemma `Set.toFinset_setOf` without the `_`
  simp [Set.toFinset_setOf _]
#align set.finite.to_finset_set_of Set.Finite.toFinset_setOf

@[simp]
nonrec theorem disjoint_toFinset {hs : s.Finite} {ht : t.Finite} :
    Disjoint hs.toFinset ht.toFinset â†” Disjoint s t :=
  @disjoint_toFinset _ _ _ hs.fintype ht.fintype
#align set.finite.disjoint_to_finset Set.Finite.disjoint_toFinset

protected theorem toFinset_inter [DecidableEq Î±] (hs : s.Finite) (ht : t.Finite)
    (h : (s âˆ© t).Finite) : h.toFinset = hs.toFinset âˆ© ht.toFinset := by
  ext
  simp
#align set.finite.to_finset_inter Set.Finite.toFinset_inter

protected theorem toFinset_union [DecidableEq Î±] (hs : s.Finite) (ht : t.Finite)
    (h : (s âˆª t).Finite) : h.toFinset = hs.toFinset âˆª ht.toFinset := by
  ext
  simp
#align set.finite.to_finset_union Set.Finite.toFinset_union

protected theorem toFinset_diff [DecidableEq Î±] (hs : s.Finite) (ht : t.Finite)
    (h : (s \ t).Finite) : h.toFinset = hs.toFinset \ ht.toFinset := by
  ext
  simp
#align set.finite.to_finset_diff Set.Finite.toFinset_diff

open scoped symmDiff in
protected theorem toFinset_symmDiff [DecidableEq Î±] (hs : s.Finite) (ht : t.Finite)
    (h : (s âˆ† t).Finite) : h.toFinset = hs.toFinset âˆ† ht.toFinset := by
  ext
  simp [mem_symmDiff, Finset.mem_symmDiff]
#align set.finite.to_finset_symm_diff Set.Finite.toFinset_symmDiff

protected theorem toFinset_compl [DecidableEq Î±] [Fintype Î±] (hs : s.Finite) (h : sá¶œ.Finite) :
    h.toFinset = hs.toFinsetá¶œ := by
  ext
  simp
#align set.finite.to_finset_compl Set.Finite.toFinset_compl

protected theorem toFinset_univ [Fintype Î±] (h : (Set.univ : Set Î±).Finite) :
    h.toFinset = Finset.univ := by
  simp
#align set.finite.to_finset_univ Set.Finite.toFinset_univ

@[simp]
protected theorem toFinset_eq_empty {h : s.Finite} : h.toFinset = âˆ… â†” s = âˆ… :=
  @toFinset_eq_empty _ _ h.fintype
#align set.finite.to_finset_eq_empty Set.Finite.toFinset_eq_empty

protected theorem toFinset_empty (h : (âˆ… : Set Î±).Finite) : h.toFinset = âˆ… := by
  simp
#align set.finite.to_finset_empty Set.Finite.toFinset_empty

@[simp]
protected theorem toFinset_eq_univ [Fintype Î±] {h : s.Finite} :
    h.toFinset = Finset.univ â†” s = univ :=
  @toFinset_eq_univ _ _ _ h.fintype
#align set.finite.to_finset_eq_univ Set.Finite.toFinset_eq_univ

protected theorem toFinset_image [DecidableEq Î²] (f : Î± â†’ Î²) (hs : s.Finite) (h : (f '' s).Finite) :
    h.toFinset = hs.toFinset.image f := by
  ext
  simp
#align set.finite.to_finset_image Set.Finite.toFinset_image

-- Porting note (#10618): now `simp` can prove it but it needs the `fintypeRange` instance
-- from the next section
protected theorem toFinset_range [DecidableEq Î±] [Fintype Î²] (f : Î² â†’ Î±) (h : (range f).Finite) :
    h.toFinset = Finset.univ.image f := by
  ext
  simp
#align set.finite.to_finset_range Set.Finite.toFinset_range

end Finite

/-! ### Fintype instances

Every instance here should have a corresponding `Set.Finite` constructor in the next section.
-/

section FintypeInstances

instance fintypeUniv [Fintype Î±] : Fintype (@univ Î±) :=
  Fintype.ofEquiv Î± (Equiv.Set.univ Î±).symm
#align set.fintype_univ Set.fintypeUniv

/-- If `(Set.univ : Set Î±)` is finite then `Î±` is a finite type. -/
noncomputable def fintypeOfFiniteUniv (H : (univ (Î± := Î±)).Finite) : Fintype Î± :=
  @Fintype.ofEquiv _ (univ : Set Î±) H.fintype (Equiv.Set.univ _)
#align set.fintype_of_finite_univ Set.fintypeOfFiniteUniv

instance fintypeUnion [DecidableEq Î±] (s t : Set Î±) [Fintype s] [Fintype t] :
    Fintype (s âˆª t : Set Î±) :=
  Fintype.ofFinset (s.toFinset âˆª t.toFinset) <| by simp
#align set.fintype_union Set.fintypeUnion

instance fintypeSep (s : Set Î±) (p : Î± â†’ Prop) [Fintype s] [DecidablePred p] :
    Fintype ({ a âˆˆ s | p a } : Set Î±) :=
  Fintype.ofFinset (s.toFinset.filter p) <| by simp
#align set.fintype_sep Set.fintypeSep

instance fintypeInter (s t : Set Î±) [DecidableEq Î±] [Fintype s] [Fintype t] :
    Fintype (s âˆ© t : Set Î±) :=
  Fintype.ofFinset (s.toFinset âˆ© t.toFinset) <| by simp
#align set.fintype_inter Set.fintypeInter

/-- A `Fintype` instance for set intersection where the left set has a `Fintype` instance. -/
instance fintypeInterOfLeft (s t : Set Î±) [Fintype s] [DecidablePred (Â· âˆˆ t)] :
    Fintype (s âˆ© t : Set Î±) :=
  Fintype.ofFinset (s.toFinset.filter (Â· âˆˆ t)) <| by simp
#align set.fintype_inter_of_left Set.fintypeInterOfLeft

/-- A `Fintype` instance for set intersection where the right set has a `Fintype` instance. -/
instance fintypeInterOfRight (s t : Set Î±) [Fintype t] [DecidablePred (Â· âˆˆ s)] :
    Fintype (s âˆ© t : Set Î±) :=
  Fintype.ofFinset (t.toFinset.filter (Â· âˆˆ s)) <| by simp [and_comm]
#align set.fintype_inter_of_right Set.fintypeInterOfRight

/-- A `Fintype` structure on a set defines a `Fintype` structure on its subset. -/
def fintypeSubset (s : Set Î±) {t : Set Î±} [Fintype s] [DecidablePred (Â· âˆˆ t)] (h : t âŠ† s) :
    Fintype t := by
  rw [â† inter_eq_self_of_subset_right h]
  apply Set.fintypeInterOfLeft
#align set.fintype_subset Set.fintypeSubset

instance fintypeDiff [DecidableEq Î±] (s t : Set Î±) [Fintype s] [Fintype t] :
    Fintype (s \ t : Set Î±) :=
  Fintype.ofFinset (s.toFinset \ t.toFinset) <| by simp
#align set.fintype_diff Set.fintypeDiff

instance fintypeDiffLeft (s t : Set Î±) [Fintype s] [DecidablePred (Â· âˆˆ t)] :
    Fintype (s \ t : Set Î±) :=
  Set.fintypeSep s (Â· âˆˆ tá¶œ)
#align set.fintype_diff_left Set.fintypeDiffLeft

instance fintypeiUnion [DecidableEq Î±] [Fintype (PLift Î¹)] (f : Î¹ â†’ Set Î±) [âˆ€ i, Fintype (f i)] :
    Fintype (â‹ƒ i, f i) :=
  Fintype.ofFinset (Finset.univ.biUnion fun i : PLift Î¹ => (f i.down).toFinset) <| by simp
#align set.fintype_Union Set.fintypeiUnion

instance fintypesUnion [DecidableEq Î±] {s : Set (Set Î±)} [Fintype s]
    [H : âˆ€ t : s, Fintype (t : Set Î±)] : Fintype (â‹ƒâ‚€ s) := by
  rw [sUnion_eq_iUnion]
  exact @Set.fintypeiUnion _ _ _ _ _ H
#align set.fintype_sUnion Set.fintypesUnion

/-- A union of sets with `Fintype` structure over a set with `Fintype` structure has a `Fintype`
structure. -/
def fintypeBiUnion [DecidableEq Î±] {Î¹ : Type*} (s : Set Î¹) [Fintype s] (t : Î¹ â†’ Set Î±)
    (H : âˆ€ i âˆˆ s, Fintype (t i)) : Fintype (â‹ƒ x âˆˆ s, t x) :=
  haveI : âˆ€ i : toFinset s, Fintype (t i) := fun i => H i (mem_toFinset.1 i.2)
  Fintype.ofFinset (s.toFinset.attach.biUnion fun x => (t x).toFinset) fun x => by simp
#align set.fintype_bUnion Set.fintypeBiUnion

instance fintypeBiUnion' [DecidableEq Î±] {Î¹ : Type*} (s : Set Î¹) [Fintype s] (t : Î¹ â†’ Set Î±)
    [âˆ€ i, Fintype (t i)] : Fintype (â‹ƒ x âˆˆ s, t x) :=
  Fintype.ofFinset (s.toFinset.biUnion fun x => (t x).toFinset) <| by simp
#align set.fintype_bUnion' Set.fintypeBiUnion'

section monad
attribute [local instance] Set.monad

/-- If `s : Set Î±` is a set with `Fintype` instance and `f : Î± â†’ Set Î²` is a function such that
each `f a`, `a âˆˆ s`, has a `Fintype` structure, then `s >>= f` has a `Fintype` structure. -/
def fintypeBind {Î± Î²} [DecidableEq Î²] (s : Set Î±) [Fintype s] (f : Î± â†’ Set Î²)
    (H : âˆ€ a âˆˆ s, Fintype (f a)) : Fintype (s >>= f) :=
  Set.fintypeBiUnion s f H
#align set.fintype_bind Set.fintypeBind

instance fintypeBind' {Î± Î²} [DecidableEq Î²] (s : Set Î±) [Fintype s] (f : Î± â†’ Set Î²)
    [âˆ€ a, Fintype (f a)] : Fintype (s >>= f) :=
  Set.fintypeBiUnion' s f
#align set.fintype_bind' Set.fintypeBind'

end monad

instance fintypeEmpty : Fintype (âˆ… : Set Î±) :=
  Fintype.ofFinset âˆ… <| by simp
#align set.fintype_empty Set.fintypeEmpty

instance fintypeSingleton (a : Î±) : Fintype ({a} : Set Î±) :=
  Fintype.ofFinset {a} <| by simp
#align set.fintype_singleton Set.fintypeSingleton

instance fintypePure : âˆ€ a : Î±, Fintype (pure a : Set Î±) :=
  Set.fintypeSingleton
#align set.fintype_pure Set.fintypePure

/-- A `Fintype` instance for inserting an element into a `Set` using the
corresponding `insert` function on `Finset`. This requires `DecidableEq Î±`.
There is also `Set.fintypeInsert'` when `a âˆˆ s` is decidable. -/
instance fintypeInsert (a : Î±) (s : Set Î±) [DecidableEq Î±] [Fintype s] :
    Fintype (insert a s : Set Î±) :=
  Fintype.ofFinset (insert a s.toFinset) <| by simp
#align set.fintype_insert Set.fintypeInsert

/-- A `Fintype` structure on `insert a s` when inserting a new element. -/
def fintypeInsertOfNotMem {a : Î±} (s : Set Î±) [Fintype s] (h : a âˆ‰ s) :
    Fintype (insert a s : Set Î±) :=
  Fintype.ofFinset âŸ¨a ::â‚˜ s.toFinset.1, s.toFinset.nodup.cons (by simp [h])âŸ© <| by simp
#align set.fintype_insert_of_not_mem Set.fintypeInsertOfNotMem

/-- A `Fintype` structure on `insert a s` when inserting a pre-existing element. -/
def fintypeInsertOfMem {a : Î±} (s : Set Î±) [Fintype s] (h : a âˆˆ s) : Fintype (insert a s : Set Î±) :=
  Fintype.ofFinset s.toFinset <| by simp [h]
#align set.fintype_insert_of_mem Set.fintypeInsertOfMem

/-- The `Set.fintypeInsert` instance requires decidable equality, but when `a âˆˆ s`
is decidable for this particular `a` we can still get a `Fintype` instance by using
`Set.fintypeInsertOfNotMem` or `Set.fintypeInsertOfMem`.

This instance pre-dates `Set.fintypeInsert`, and it is less efficient.
When `Set.decidableMemOfFintype` is made a local instance, then this instance would
override `Set.fintypeInsert` if not for the fact that its priority has been
adjusted. See Note [lower instance priority]. -/
instance (priority := 100) fintypeInsert' (a : Î±) (s : Set Î±) [Decidable <| a âˆˆ s] [Fintype s] :
    Fintype (insert a s : Set Î±) :=
  if h : a âˆˆ s then fintypeInsertOfMem s h else fintypeInsertOfNotMem s h
#align set.fintype_insert' Set.fintypeInsert'

instance fintypeImage [DecidableEq Î²] (s : Set Î±) (f : Î± â†’ Î²) [Fintype s] : Fintype (f '' s) :=
  Fintype.ofFinset (s.toFinset.image f) <| by simp
#align set.fintype_image Set.fintypeImage

/-- If a function `f` has a partial inverse and sends a set `s` to a set with `[Fintype]` instance,
then `s` has a `Fintype` structure as well. -/
def fintypeOfFintypeImage (s : Set Î±) {f : Î± â†’ Î²} {g} (I : IsPartialInv f g) [Fintype (f '' s)] :
    Fintype s :=
  Fintype.ofFinset âŸ¨_, (f '' s).toFinset.2.filterMap g <| injective_of_isPartialInv_right IâŸ©
    fun a => by
    suffices (âˆƒ b x, f x = b âˆ§ g b = some a âˆ§ x âˆˆ s) â†” a âˆˆ s by
      simpa [exists_and_left.symm, and_comm, and_left_comm, and_assoc]
    rw [exists_swap]
    suffices (âˆƒ x, x âˆˆ s âˆ§ g (f x) = some a) â†” a âˆˆ s by simpa [and_comm, and_left_comm, and_assoc]
    simp [I _, (injective_of_isPartialInv I).eq_iff]
#align set.fintype_of_fintype_image Set.fintypeOfFintypeImage

instance fintypeRange [DecidableEq Î±] (f : Î¹ â†’ Î±) [Fintype (PLift Î¹)] : Fintype (range f) :=
  Fintype.ofFinset (Finset.univ.image <| f âˆ˜ PLift.down) <| by simp
#align set.fintype_range Set.fintypeRange

instance fintypeMap {Î± Î²} [DecidableEq Î²] :
    âˆ€ (s : Set Î±) (f : Î± â†’ Î²) [Fintype s], Fintype (f <$> s) :=
  Set.fintypeImage
#align set.fintype_map Set.fintypeMap

instance fintypeLTNat (n : â„•) : Fintype { i | i < n } :=
  Fintype.ofFinset (Finset.range n) <| by simp
#align set.fintype_lt_nat Set.fintypeLTNat

instance fintypeLENat (n : â„•) : Fintype { i | i â‰¤ n } := by
  simpa [Nat.lt_succ_iff] using Set.fintypeLTNat (n + 1)
#align set.fintype_le_nat Set.fintypeLENat

/-- This is not an instance so that it does not conflict with the one
in `Mathlib/Order/LocallyFinite.lean`. -/
def Nat.fintypeIio (n : â„•) : Fintype (Iio n) :=
  Set.fintypeLTNat n
#align set.nat.fintype_Iio Set.Nat.fintypeIio

instance fintypeProd (s : Set Î±) (t : Set Î²) [Fintype s] [Fintype t] :
    Fintype (s Ã—Ë¢ t : Set (Î± Ã— Î²)) :=
  Fintype.ofFinset (s.toFinset Ã—Ë¢ t.toFinset) <| by simp
#align set.fintype_prod Set.fintypeProd

instance fintypeOffDiag [DecidableEq Î±] (s : Set Î±) [Fintype s] : Fintype s.offDiag :=
  Fintype.ofFinset s.toFinset.offDiag <| by simp
#align set.fintype_off_diag Set.fintypeOffDiag

/-- `image2 f s t` is `Fintype` if `s` and `t` are. -/
instance fintypeImage2 [DecidableEq Î³] (f : Î± â†’ Î² â†’ Î³) (s : Set Î±) (t : Set Î²) [hs : Fintype s]
    [ht : Fintype t] : Fintype (image2 f s t : Set Î³) := by
  rw [â† image_prod]
  apply Set.fintypeImage
#align set.fintype_image2 Set.fintypeImage2

instance fintypeSeq [DecidableEq Î²] (f : Set (Î± â†’ Î²)) (s : Set Î±) [Fintype f] [Fintype s] :
    Fintype (f.seq s) := by
  rw [seq_def]
  apply Set.fintypeBiUnion'
#align set.fintype_seq Set.fintypeSeq

instance fintypeSeq' {Î± Î² : Type u} [DecidableEq Î²] (f : Set (Î± â†’ Î²)) (s : Set Î±) [Fintype f]
    [Fintype s] : Fintype (f <*> s) :=
  Set.fintypeSeq f s
#align set.fintype_seq' Set.fintypeSeq'

instance fintypeMemFinset (s : Finset Î±) : Fintype { a | a âˆˆ s } :=
  Finset.fintypeCoeSort s
#align set.fintype_mem_finset Set.fintypeMemFinset

end FintypeInstances

end Set

theorem Equiv.set_finite_iff {s : Set Î±} {t : Set Î²} (hst : s â‰ƒ t) : s.Finite â†” t.Finite := by
  simp_rw [â† Set.finite_coe_iff, hst.finite_iff]
#align equiv.set_finite_iff Equiv.set_finite_iff

/-! ### Finset -/

namespace Finset

/-- Gives a `Set.Finite` for the `Finset` coerced to a `Set`.
This is a wrapper around `Set.toFinite`. -/
@[simp]
theorem finite_toSet (s : Finset Î±) : (s : Set Î±).Finite :=
  Set.toFinite _
#align finset.finite_to_set Finset.finite_toSet

-- Porting note (#10618): was @[simp], now `simp` can prove it
theorem finite_toSet_toFinset (s : Finset Î±) : s.finite_toSet.toFinset = s := by
  rw [toFinite_toFinset, toFinset_coe]
#align finset.finite_to_set_to_finset Finset.finite_toSet_toFinset

end Finset

namespace Multiset

@[simp]
theorem finite_toSet (s : Multiset Î±) : { x | x âˆˆ s }.Finite := by
  classical simpa only [â† Multiset.mem_toFinset] using s.toFinset.finite_toSet
#align multiset.finite_to_set Multiset.finite_toSet

@[simp]
theorem finite_toSet_toFinset [DecidableEq Î±] (s : Multiset Î±) :
    s.finite_toSet.toFinset = s.toFinset := by
  ext x
  simp
#align multiset.finite_to_set_to_finset Multiset.finite_toSet_toFinset

end Multiset

@[simp]
theorem List.finite_toSet (l : List Î±) : { x | x âˆˆ l }.Finite :=
  (show Multiset Î± from âŸ¦lâŸ§).finite_toSet
#align list.finite_to_set List.finite_toSet

/-! ### Finite instances

There is seemingly some overlap between the following instances and the `Fintype` instances
in `Data.Set.Finite`. While every `Fintype` instance gives a `Finite` instance, those
instances that depend on `Fintype` or `Decidable` instances need an additional `Finite` instance
to be able to generally apply.

Some set instances do not appear here since they are consequences of others, for example
`Subtype.Finite` for subsets of a finite type.
-/


namespace Finite.Set

open scoped Classical

example {s : Set Î±} [Finite Î±] : Finite s :=
  inferInstance

example : Finite (âˆ… : Set Î±) :=
  inferInstance

example (a : Î±) : Finite ({a} : Set Î±) :=
  inferInstance

instance finite_union (s t : Set Î±) [Finite s] [Finite t] : Finite (s âˆª t : Set Î±) := by
  cases nonempty_fintype s
  cases nonempty_fintype t
  infer_instance
#align finite.set.finite_union Finite.Set.finite_union

instance finite_sep (s : Set Î±) (p : Î± â†’ Prop) [Finite s] : Finite ({ a âˆˆ s | p a } : Set Î±) := by
  cases nonempty_fintype s
  infer_instance
#align finite.set.finite_sep Finite.Set.finite_sep

protected theorem subset (s : Set Î±) {t : Set Î±} [Finite s] (h : t âŠ† s) : Finite t := by
  rw [â† sep_eq_of_subset h]
  infer_instance
#align finite.set.subset Finite.Set.subset

instance finite_inter_of_right (s t : Set Î±) [Finite t] : Finite (s âˆ© t : Set Î±) :=
  Finite.Set.subset t inter_subset_right
#align finite.set.finite_inter_of_right Finite.Set.finite_inter_of_right

instance finite_inter_of_left (s t : Set Î±) [Finite s] : Finite (s âˆ© t : Set Î±) :=
  Finite.Set.subset s inter_subset_left
#align finite.set.finite_inter_of_left Finite.Set.finite_inter_of_left

instance finite_diff (s t : Set Î±) [Finite s] : Finite (s \ t : Set Î±) :=
  Finite.Set.subset s diff_subset
#align finite.set.finite_diff Finite.Set.finite_diff

instance finite_range (f : Î¹ â†’ Î±) [Finite Î¹] : Finite (range f) := by
  haveI := Fintype.ofFinite (PLift Î¹)
  infer_instance
#align finite.set.finite_range Finite.Set.finite_range

instance finite_iUnion [Finite Î¹] (f : Î¹ â†’ Set Î±) [âˆ€ i, Finite (f i)] : Finite (â‹ƒ i, f i) := by
  rw [iUnion_eq_range_psigma]
  apply Set.finite_range
#align finite.set.finite_Union Finite.Set.finite_iUnion

instance finite_sUnion {s : Set (Set Î±)} [Finite s] [H : âˆ€ t : s, Finite (t : Set Î±)] :
    Finite (â‹ƒâ‚€ s) := by
  rw [sUnion_eq_iUnion]
  exact @Finite.Set.finite_iUnion _ _ _ _ H
#align finite.set.finite_sUnion Finite.Set.finite_sUnion

theorem finite_biUnion {Î¹ : Type*} (s : Set Î¹) [Finite s] (t : Î¹ â†’ Set Î±)
    (H : âˆ€ i âˆˆ s, Finite (t i)) : Finite (â‹ƒ x âˆˆ s, t x) := by
  rw [biUnion_eq_iUnion]
  haveI : âˆ€ i : s, Finite (t i) := fun i => H i i.property
  infer_instance
#align finite.set.finite_bUnion Finite.Set.finite_biUnion

instance finite_biUnion' {Î¹ : Type*} (s : Set Î¹) [Finite s] (t : Î¹ â†’ Set Î±) [âˆ€ i, Finite (t i)] :
    Finite (â‹ƒ x âˆˆ s, t x) :=
  finite_biUnion s t fun _ _ => inferInstance
#align finite.set.finite_bUnion' Finite.Set.finite_biUnion'

/-- Example: `Finite (â‹ƒ (i < n), f i)` where `f : â„• â†’ Set Î±` and `[âˆ€ i, Finite (f i)]`
(when given instances from `Order.Interval.Finset.Nat`).
-/
instance finite_biUnion'' {Î¹ : Type*} (p : Î¹ â†’ Prop) [h : Finite { x | p x }] (t : Î¹ â†’ Set Î±)
    [âˆ€ i, Finite (t i)] : Finite (â‹ƒ (x) (_ : p x), t x) :=
  @Finite.Set.finite_biUnion' _ _ (setOf p) h t _
#align finite.set.finite_bUnion'' Finite.Set.finite_biUnion''

instance finite_iInter {Î¹ : Sort*} [Nonempty Î¹] (t : Î¹ â†’ Set Î±) [âˆ€ i, Finite (t i)] :
    Finite (â‹‚ i, t i) :=
  Finite.Set.subset (t <| Classical.arbitrary Î¹) (iInter_subset _ _)
#align finite.set.finite_Inter Finite.Set.finite_iInter

instance finite_insert (a : Î±) (s : Set Î±) [Finite s] : Finite (insert a s : Set Î±) :=
  Finite.Set.finite_union {a} s
#align finite.set.finite_insert Finite.Set.finite_insert

instance finite_image (s : Set Î±) (f : Î± â†’ Î²) [Finite s] : Finite (f '' s) := by
  cases nonempty_fintype s
  infer_instance
#align finite.set.finite_image Finite.Set.finite_image

instance finite_replacement [Finite Î±] (f : Î± â†’ Î²) :
    Finite {f x | x : Î±} :=
  Finite.Set.finite_range f
#align finite.set.finite_replacement Finite.Set.finite_replacement

instance finite_prod (s : Set Î±) (t : Set Î²) [Finite s] [Finite t] :
    Finite (s Ã—Ë¢ t : Set (Î± Ã— Î²)) :=
  Finite.of_equiv _ (Equiv.Set.prod s t).symm
#align finite.set.finite_prod Finite.Set.finite_prod

instance finite_image2 (f : Î± â†’ Î² â†’ Î³) (s : Set Î±) (t : Set Î²) [Finite s] [Finite t] :
    Finite (image2 f s t : Set Î³) := by
  rw [â† image_prod]
  infer_instance
#align finite.set.finite_image2 Finite.Set.finite_image2

instance finite_seq (f : Set (Î± â†’ Î²)) (s : Set Î±) [Finite f] [Finite s] : Finite (f.seq s) := by
  rw [seq_def]
  infer_instance
#align finite.set.finite_seq Finite.Set.finite_seq

end Finite.Set

namespace Set

/-! ### Constructors for `Set.Finite`

Every constructor here should have a corresponding `Fintype` instance in the previous section
(or in the `Fintype` module).

The implementation of these constructors ideally should be no more than `Set.toFinite`,
after possibly setting up some `Fintype` and classical `Decidable` instances.
-/


section SetFiniteConstructors

@[nontriviality]
theorem Finite.of_subsingleton [Subsingleton Î±] (s : Set Î±) : s.Finite :=
  s.toFinite
#align set.finite.of_subsingleton Set.Finite.of_subsingleton

theorem finite_univ [Finite Î±] : (@univ Î±).Finite :=
  Set.toFinite _
#align set.finite_univ Set.finite_univ

theorem finite_univ_iff : (@univ Î±).Finite â†” Finite Î± := (Equiv.Set.univ Î±).finite_iff
#align set.finite_univ_iff Set.finite_univ_iff

alias âŸ¨_root_.Finite.of_finite_univ, _âŸ© := finite_univ_iff
#align finite.of_finite_univ Finite.of_finite_univ

theorem Finite.subset {s : Set Î±} (hs : s.Finite) {t : Set Î±} (ht : t âŠ† s) : t.Finite := by
  have := hs.to_subtype
  exact Finite.Set.subset _ ht
#align set.finite.subset Set.Finite.subset

theorem Finite.union {s t : Set Î±} (hs : s.Finite) (ht : t.Finite) : (s âˆª t).Finite := by
  rw [Set.Finite] at hs ht
  apply toFinite
#align set.finite.union Set.Finite.union

theorem Finite.finite_of_compl {s : Set Î±} (hs : s.Finite) (hsc : sá¶œ.Finite) : Finite Î± := by
  rw [â† finite_univ_iff, â† union_compl_self s]
  exact hs.union hsc
#align set.finite.finite_of_compl Set.Finite.finite_of_compl

theorem Finite.sup {s t : Set Î±} : s.Finite â†’ t.Finite â†’ (s âŠ” t).Finite :=
  Finite.union
#align set.finite.sup Set.Finite.sup

theorem Finite.sep {s : Set Î±} (hs : s.Finite) (p : Î± â†’ Prop) : { a âˆˆ s | p a }.Finite :=
  hs.subset <| sep_subset _ _
#align set.finite.sep Set.Finite.sep

theorem Finite.inter_of_left {s : Set Î±} (hs : s.Finite) (t : Set Î±) : (s âˆ© t).Finite :=
  hs.subset inter_subset_left
#align set.finite.inter_of_left Set.Finite.inter_of_left

theorem Finite.inter_of_right {s : Set Î±} (hs : s.Finite) (t : Set Î±) : (t âˆ© s).Finite :=
  hs.subset inter_subset_right
#align set.finite.inter_of_right Set.Finite.inter_of_right

theorem Finite.inf_of_left {s : Set Î±} (h : s.Finite) (t : Set Î±) : (s âŠ“ t).Finite :=
  h.inter_of_left t
#align set.finite.inf_of_left Set.Finite.inf_of_left

theorem Finite.inf_of_right {s : Set Î±} (h : s.Finite) (t : Set Î±) : (t âŠ“ s).Finite :=
  h.inter_of_right t
#align set.finite.inf_of_right Set.Finite.inf_of_right

protected lemma Infinite.mono {s t : Set Î±} (h : s âŠ† t) : s.Infinite â†’ t.Infinite :=
  mt fun ht â†¦ ht.subset h
#align set.infinite.mono Set.Infinite.mono

theorem Finite.diff {s : Set Î±} (hs : s.Finite) (t : Set Î±) : (s \ t).Finite :=
  hs.subset diff_subset
#align set.finite.diff Set.Finite.diff

theorem Finite.of_diff {s t : Set Î±} (hd : (s \ t).Finite) (ht : t.Finite) : s.Finite :=
  (hd.union ht).subset <| subset_diff_union _ _
#align set.finite.of_diff Set.Finite.of_diff

theorem finite_iUnion [Finite Î¹] {f : Î¹ â†’ Set Î±} (H : âˆ€ i, (f i).Finite) : (â‹ƒ i, f i).Finite :=
  haveI := fun i => (H i).to_subtype
  toFinite _
#align set.finite_Union Set.finite_iUnion

/-- Dependent version of `Finite.biUnion`. -/
theorem Finite.biUnion' {Î¹} {s : Set Î¹} (hs : s.Finite) {t : âˆ€ i âˆˆ s, Set Î±}
    (ht : âˆ€ i (hi : i âˆˆ s), (t i hi).Finite) : (â‹ƒ i âˆˆ s, t i â€¹_â€º).Finite := by
  have := hs.to_subtype
  rw [biUnion_eq_iUnion]
  apply finite_iUnion fun i : s => ht i.1 i.2
#align set.finite.bUnion' Set.Finite.biUnion'

theorem Finite.biUnion {Î¹} {s : Set Î¹} (hs : s.Finite) {t : Î¹ â†’ Set Î±}
    (ht : âˆ€ i âˆˆ s, (t i).Finite) : (â‹ƒ i âˆˆ s, t i).Finite :=
  hs.biUnion' ht
#align set.finite.bUnion Set.Finite.biUnion

theorem Finite.sUnion {s : Set (Set Î±)} (hs : s.Finite) (H : âˆ€ t âˆˆ s, Set.Finite t) :
    (â‹ƒâ‚€ s).Finite := by
  simpa only [sUnion_eq_biUnion] using hs.biUnion H
#align set.finite.sUnion Set.Finite.sUnion

theorem Finite.sInter {Î± : Type*} {s : Set (Set Î±)} {t : Set Î±} (ht : t âˆˆ s) (hf : t.Finite) :
    (â‹‚â‚€ s).Finite :=
  hf.subset (sInter_subset_of_mem ht)
#align set.finite.sInter Set.Finite.sInter

/-- If sets `s i` are finite for all `i` from a finite set `t` and are empty for `i âˆ‰ t`, then the
union `â‹ƒ i, s i` is a finite set. -/
theorem Finite.iUnion {Î¹ : Type*} {s : Î¹ â†’ Set Î±} {t : Set Î¹} (ht : t.Finite)
    (hs : âˆ€ i âˆˆ t, (s i).Finite) (he : âˆ€ i, i âˆ‰ t â†’ s i = âˆ…) : (â‹ƒ i, s i).Finite := by
  suffices â‹ƒ i, s i âŠ† â‹ƒ i âˆˆ t, s i by exact (ht.biUnion hs).subset this
  refine iUnion_subset fun i x hx => ?_
  by_cases hi : i âˆˆ t
  Â· exact mem_biUnion hi hx
  Â· rw [he i hi, mem_empty_iff_false] at hx
    contradiction
#align set.finite.Union Set.Finite.iUnion

section monad
attribute [local instance] Set.monad

theorem Finite.bind {Î± Î²} {s : Set Î±} {f : Î± â†’ Set Î²} (h : s.Finite) (hf : âˆ€ a âˆˆ s, (f a).Finite) :
    (s >>= f).Finite :=
  h.biUnion hf
#align set.finite.bind Set.Finite.bind

end monad

@[simp]
theorem finite_empty : (âˆ… : Set Î±).Finite :=
  toFinite _
#align set.finite_empty Set.finite_empty

protected theorem Infinite.nonempty {s : Set Î±} (h : s.Infinite) : s.Nonempty :=
  nonempty_iff_ne_empty.2 <| by
    rintro rfl
    exact h finite_empty
#align set.infinite.nonempty Set.Infinite.nonempty

@[simp]
theorem finite_singleton (a : Î±) : ({a} : Set Î±).Finite :=
  toFinite _
#align set.finite_singleton Set.finite_singleton

theorem finite_pure (a : Î±) : (pure a : Set Î±).Finite :=
  toFinite _
#align set.finite_pure Set.finite_pure

@[simp]
protected theorem Finite.insert (a : Î±) {s : Set Î±} (hs : s.Finite) : (insert a s).Finite :=
  (finite_singleton a).union hs
#align set.finite.insert Set.Finite.insert

theorem Finite.image {s : Set Î±} (f : Î± â†’ Î²) (hs : s.Finite) : (f '' s).Finite := by
  have := hs.to_subtype
  apply toFinite
#align set.finite.image Set.Finite.image

theorem finite_range (f : Î¹ â†’ Î±) [Finite Î¹] : (range f).Finite :=
  toFinite _
#align set.finite_range Set.finite_range

lemma Finite.of_surjOn {s : Set Î±} {t : Set Î²} (f : Î± â†’ Î²) (hf : SurjOn f s t) (hs : s.Finite) :
    t.Finite := (hs.image _).subset hf

theorem Finite.dependent_image {s : Set Î±} (hs : s.Finite) (F : âˆ€ i âˆˆ s, Î²) :
    {y : Î² | âˆƒ x hx, F x hx = y}.Finite := by
  have := hs.to_subtype
  simpa [range] using finite_range fun x : s => F x x.2
#align set.finite.dependent_image Set.Finite.dependent_image

theorem Finite.map {Î± Î²} {s : Set Î±} : âˆ€ f : Î± â†’ Î², s.Finite â†’ (f <$> s).Finite :=
  Finite.image
#align set.finite.map Set.Finite.map

theorem Finite.of_finite_image {s : Set Î±} {f : Î± â†’ Î²} (h : (f '' s).Finite) (hi : Set.InjOn f s) :
    s.Finite :=
  have := h.to_subtype
  .of_injective _ hi.bijOn_image.bijective.injective
#align set.finite.of_finite_image Set.Finite.of_finite_image

section preimage
variable {f : Î± â†’ Î²} {s : Set Î²}

theorem finite_of_finite_preimage (h : (f â»Â¹' s).Finite) (hs : s âŠ† range f) : s.Finite := by
  rw [â† image_preimage_eq_of_subset hs]
  exact Finite.image f h
#align set.finite_of_finite_preimage Set.finite_of_finite_preimage

theorem Finite.of_preimage (h : (f â»Â¹' s).Finite) (hf : Surjective f) : s.Finite :=
  hf.image_preimage s â–¸ h.image _
#align set.finite.of_preimage Set.Finite.of_preimage

theorem Finite.preimage (I : Set.InjOn f (f â»Â¹' s)) (h : s.Finite) : (f â»Â¹' s).Finite :=
  (h.subset (image_preimage_subset f s)).of_finite_image I
#align set.finite.preimage Set.Finite.preimage

protected lemma Infinite.preimage (hs : s.Infinite) (hf : s âŠ† range f) : (f â»Â¹' s).Infinite :=
  fun h â†¦ hs <| finite_of_finite_preimage h hf

lemma Infinite.preimage' (hs : (s âˆ© range f).Infinite) : (f â»Â¹' s).Infinite :=
  (hs.preimage inter_subset_right).mono <| preimage_mono inter_subset_left

theorem Finite.preimage_embedding {s : Set Î²} (f : Î± â†ª Î²) (h : s.Finite) : (f â»Â¹' s).Finite :=
  h.preimage fun _ _ _ _ h' => f.injective h'
#align set.finite.preimage_embedding Set.Finite.preimage_embedding

end preimage

theorem finite_lt_nat (n : â„•) : Set.Finite { i | i < n } :=
  toFinite _
#align set.finite_lt_nat Set.finite_lt_nat

theorem finite_le_nat (n : â„•) : Set.Finite { i | i â‰¤ n } :=
  toFinite _
#align set.finite_le_nat Set.finite_le_nat

section MapsTo

variable {s : Set Î±} {f : Î± â†’ Î±} (hs : s.Finite) (hm : MapsTo f s s)

theorem Finite.surjOn_iff_bijOn_of_mapsTo : SurjOn f s s â†” BijOn f s s := by
  refine âŸ¨fun h â†¦ âŸ¨hm, ?_, hâŸ©, BijOn.surjOnâŸ©
  have : Finite s := finite_coe_iff.mpr hs
  exact hm.restrict_inj.mp (Finite.injective_iff_surjective.mpr <| hm.restrict_surjective_iff.mpr h)

theorem Finite.injOn_iff_bijOn_of_mapsTo : InjOn f s â†” BijOn f s s := by
  refine âŸ¨fun h â†¦ âŸ¨hm, h, ?_âŸ©, BijOn.injOnâŸ©
  have : Finite s := finite_coe_iff.mpr hs
  exact hm.restrict_surjective_iff.mp (Finite.injective_iff_surjective.mp <| hm.restrict_inj.mpr h)

end MapsTo

section Prod

variable {s : Set Î±} {t : Set Î²}

protected theorem Finite.prod (hs : s.Finite) (ht : t.Finite) : (s Ã—Ë¢ t : Set (Î± Ã— Î²)).Finite := by
  have := hs.to_subtype
  have := ht.to_subtype
  apply toFinite
#align set.finite.prod Set.Finite.prod

theorem Finite.of_prod_left (h : (s Ã—Ë¢ t : Set (Î± Ã— Î²)).Finite) : t.Nonempty â†’ s.Finite :=
  fun âŸ¨b, hbâŸ© => (h.image Prod.fst).subset fun a ha => âŸ¨(a, b), âŸ¨ha, hbâŸ©, rflâŸ©
#align set.finite.of_prod_left Set.Finite.of_prod_left

theorem Finite.of_prod_right (h : (s Ã—Ë¢ t : Set (Î± Ã— Î²)).Finite) : s.Nonempty â†’ t.Finite :=
  fun âŸ¨a, haâŸ© => (h.image Prod.snd).subset fun b hb => âŸ¨(a, b), âŸ¨ha, hbâŸ©, rflâŸ©
#align set.finite.of_prod_right Set.Finite.of_prod_right

protected theorem Infinite.prod_left (hs : s.Infinite) (ht : t.Nonempty) : (s Ã—Ë¢ t).Infinite :=
  fun h => hs <| h.of_prod_left ht
#align set.infinite.prod_left Set.Infinite.prod_left

protected theorem Infinite.prod_right (ht : t.Infinite) (hs : s.Nonempty) : (s Ã—Ë¢ t).Infinite :=
  fun h => ht <| h.of_prod_right hs
#align set.infinite.prod_right Set.Infinite.prod_right

protected theorem infinite_prod :
    (s Ã—Ë¢ t).Infinite â†” s.Infinite âˆ§ t.Nonempty âˆ¨ t.Infinite âˆ§ s.Nonempty := by
  refine âŸ¨fun h => ?_, ?_âŸ©
  Â· simp_rw [Set.Infinite, @and_comm Â¬_, â† Classical.not_imp]
    by_contra!
    exact h ((this.1 h.nonempty.snd).prod <| this.2 h.nonempty.fst)
  Â· rintro (h | h)
    Â· exact h.1.prod_left h.2
    Â· exact h.1.prod_right h.2
#align set.infinite_prod Set.infinite_prod

theorem finite_prod : (s Ã—Ë¢ t).Finite â†” (s.Finite âˆ¨ t = âˆ…) âˆ§ (t.Finite âˆ¨ s = âˆ…) := by
  simp only [â† not_infinite, Set.infinite_prod, not_or, not_and_or, not_nonempty_iff_eq_empty]
#align set.finite_prod Set.finite_prod

protected theorem Finite.offDiag {s : Set Î±} (hs : s.Finite) : s.offDiag.Finite :=
  (hs.prod hs).subset s.offDiag_subset_prod
#align set.finite.off_diag Set.Finite.offDiag

protected theorem Finite.image2 (f : Î± â†’ Î² â†’ Î³) (hs : s.Finite) (ht : t.Finite) :
    (image2 f s t).Finite := by
  have := hs.to_subtype
  have := ht.to_subtype
  apply toFinite
#align set.finite.image2 Set.Finite.image2

end Prod

theorem Finite.seq {f : Set (Î± â†’ Î²)} {s : Set Î±} (hf : f.Finite) (hs : s.Finite) :
    (f.seq s).Finite :=
  hf.image2 _ hs
#align set.finite.seq Set.Finite.seq

theorem Finite.seq' {Î± Î² : Type u} {f : Set (Î± â†’ Î²)} {s : Set Î±} (hf : f.Finite) (hs : s.Finite) :
    (f <*> s).Finite :=
  hf.seq hs
#align set.finite.seq' Set.Finite.seq'

theorem finite_mem_finset (s : Finset Î±) : { a | a âˆˆ s }.Finite :=
  toFinite _
#align set.finite_mem_finset Set.finite_mem_finset

theorem Subsingleton.finite {s : Set Î±} (h : s.Subsingleton) : s.Finite :=
  h.induction_on finite_empty finite_singleton
#align set.subsingleton.finite Set.Subsingleton.finite

theorem Infinite.nontrivial {s : Set Î±} (hs : s.Infinite) : s.Nontrivial :=
  not_subsingleton_iff.1 <| mt Subsingleton.finite hs

theorem finite_preimage_inl_and_inr {s : Set (Sum Î± Î²)} :
    (Sum.inl â»Â¹' s).Finite âˆ§ (Sum.inr â»Â¹' s).Finite â†” s.Finite :=
  âŸ¨fun h => image_preimage_inl_union_image_preimage_inr s â–¸ (h.1.image _).union (h.2.image _),
    fun h => âŸ¨h.preimage Sum.inl_injective.injOn, h.preimage Sum.inr_injective.injOnâŸ©âŸ©
#align set.finite_preimage_inl_and_inr Set.finite_preimage_inl_and_inr

theorem exists_finite_iff_finset {p : Set Î± â†’ Prop} :
    (âˆƒ s : Set Î±, s.Finite âˆ§ p s) â†” âˆƒ s : Finset Î±, p â†‘s :=
  âŸ¨fun âŸ¨_, hs, hpsâŸ© => âŸ¨hs.toFinset, hs.coe_toFinset.symm â–¸ hpsâŸ©, fun âŸ¨s, hsâŸ© =>
    âŸ¨s, s.finite_toSet, hsâŸ©âŸ©
#align set.exists_finite_iff_finset Set.exists_finite_iff_finset

/-- There are finitely many subsets of a given finite set -/
theorem Finite.finite_subsets {Î± : Type u} {a : Set Î±} (h : a.Finite) : { b | b âŠ† a }.Finite := by
  convert ((Finset.powerset h.toFinset).map Finset.coeEmb.1).finite_toSet
  ext s
  simpa [â† @exists_finite_iff_finset Î± fun t => t âŠ† a âˆ§ t = s, Finite.subset_toFinset,
    â† and_assoc, Finset.coeEmb] using h.subset
#align set.finite.finite_subsets Set.Finite.finite_subsets

protected theorem Finite.powerset {s : Set Î±} (h : s.Finite) : (ğ’« s).Finite :=
  h.finite_subsets

theorem exists_subset_image_finite_and {f : Î± â†’ Î²} {s : Set Î±} {p : Set Î² â†’ Prop} :
    (âˆƒ t âŠ† f '' s, t.Finite âˆ§ p t) â†” âˆƒ t âŠ† s, t.Finite âˆ§ p (f '' t) := by
  classical
  simp_rw [@and_comm (_ âŠ† _), and_assoc, exists_finite_iff_finset, @and_comm (p _),
    Finset.subset_image_iff]
  aesop

section Pi
variable {Î¹ : Type*} [Finite Î¹] {Îº : Î¹ â†’ Type*} {t : âˆ€ i, Set (Îº i)}

/-- Finite product of finite sets is finite -/
theorem Finite.pi (ht : âˆ€ i, (t i).Finite) : (pi univ t).Finite := by
  cases nonempty_fintype Î¹
  lift t to âˆ€ d, Finset (Îº d) using ht
  classical
    rw [â† Fintype.coe_piFinset]
    apply Finset.finite_toSet
#align set.finite.pi Set.Finite.pi

/-- Finite product of finite sets is finite. Note this is a variant of `Set.Finite.pi` without the
extra `i âˆˆ univ` binder. -/
lemma Finite.pi' (ht : âˆ€ i, (t i).Finite) : {f : âˆ€ i, Îº i | âˆ€ i, f i âˆˆ t i}.Finite := by
  simpa [Set.pi] using Finite.pi ht

end Pi

/-- A finite union of finsets is finite. -/
theorem union_finset_finite_of_range_finite (f : Î± â†’ Finset Î²) (h : (range f).Finite) :
    (â‹ƒ a, (f a : Set Î²)).Finite := by
  rw [â† biUnion_range]
  exact h.biUnion fun y _ => y.finite_toSet
#align set.union_finset_finite_of_range_finite Set.union_finset_finite_of_range_finite

theorem finite_range_ite {p : Î± â†’ Prop} [DecidablePred p] {f g : Î± â†’ Î²} (hf : (range f).Finite)
    (hg : (range g).Finite) : (range fun x => if p x then f x else g x).Finite :=
  (hf.union hg).subset range_ite_subset
#align set.finite_range_ite Set.finite_range_ite

theorem finite_range_const {c : Î²} : (range fun _ : Î± => c).Finite :=
  (finite_singleton c).subset range_const_subset
#align set.finite_range_const Set.finite_range_const

end SetFiniteConstructors

/-! ### Properties -/

instance Finite.inhabited : Inhabited { s : Set Î± // s.Finite } :=
  âŸ¨âŸ¨âˆ…, finite_emptyâŸ©âŸ©
#align set.finite.inhabited Set.Finite.inhabited

@[simp]
theorem finite_union {s t : Set Î±} : (s âˆª t).Finite â†” s.Finite âˆ§ t.Finite :=
  âŸ¨fun h => âŸ¨h.subset subset_union_left, h.subset subset_union_rightâŸ©, fun âŸ¨hs, htâŸ© =>
    hs.union htâŸ©
#align set.finite_union Set.finite_union

theorem finite_image_iff {s : Set Î±} {f : Î± â†’ Î²} (hi : InjOn f s) : (f '' s).Finite â†” s.Finite :=
  âŸ¨fun h => h.of_finite_image hi, Finite.image _âŸ©
#align set.finite_image_iff Set.finite_image_iff

theorem univ_finite_iff_nonempty_fintype : (univ : Set Î±).Finite â†” Nonempty (Fintype Î±) :=
  âŸ¨fun h => âŸ¨fintypeOfFiniteUniv hâŸ©, fun âŸ¨_iâŸ© => finite_univâŸ©
#align set.univ_finite_iff_nonempty_fintype Set.univ_finite_iff_nonempty_fintype

-- Porting note: moved `@[simp]` to `Set.toFinset_singleton` because `simp` can now simplify LHS
theorem Finite.toFinset_singleton {a : Î±} (ha : ({a} : Set Î±).Finite := finite_singleton _) :
    ha.toFinset = {a} :=
  Set.toFinite_toFinset _
#align set.finite.to_finset_singleton Set.Finite.toFinset_singleton

@[simp]
theorem Finite.toFinset_insert [DecidableEq Î±] {s : Set Î±} {a : Î±} (hs : (insert a s).Finite) :
    hs.toFinset = insert a (hs.subset <| subset_insert _ _).toFinset :=
  Finset.ext <| by simp
#align set.finite.to_finset_insert Set.Finite.toFinset_insert

theorem Finite.toFinset_insert' [DecidableEq Î±] {a : Î±} {s : Set Î±} (hs : s.Finite) :
    (hs.insert a).toFinset = insert a hs.toFinset :=
  Finite.toFinset_insert _
#align set.finite.to_finset_insert' Set.Finite.toFinset_insert'

theorem Finite.toFinset_prod {s : Set Î±} {t : Set Î²} (hs : s.Finite) (ht : t.Finite) :
    hs.toFinset Ã—Ë¢ ht.toFinset = (hs.prod ht).toFinset :=
  Finset.ext <| by simp
#align set.finite.to_finset_prod Set.Finite.toFinset_prod

theorem Finite.toFinset_offDiag {s : Set Î±} [DecidableEq Î±] (hs : s.Finite) :
    hs.offDiag.toFinset = hs.toFinset.offDiag :=
  Finset.ext <| by simp
#align set.finite.to_finset_off_diag Set.Finite.toFinset_offDiag

theorem Finite.fin_embedding {s : Set Î±} (h : s.Finite) :
    âˆƒ (n : â„•) (f : Fin n â†ª Î±), range f = s :=
  âŸ¨_, (Fintype.equivFin (h.toFinset : Set Î±)).symm.asEmbedding, by
    simp only [Finset.coe_sort_coe, Equiv.asEmbedding_range, Finite.coe_toFinset, setOf_mem_eq]âŸ©
#align set.finite.fin_embedding Set.Finite.fin_embedding

theorem Finite.fin_param {s : Set Î±} (h : s.Finite) :
    âˆƒ (n : â„•) (f : Fin n â†’ Î±), Injective f âˆ§ range f = s :=
  let âŸ¨n, f, hfâŸ© := h.fin_embedding
  âŸ¨n, f, f.injective, hfâŸ©
#align set.finite.fin_param Set.Finite.fin_param

theorem finite_option {s : Set (Option Î±)} : s.Finite â†” { x : Î± | some x âˆˆ s }.Finite :=
  âŸ¨fun h => h.preimage_embedding Embedding.some, fun h =>
    ((h.image some).insert none).subset fun x =>
      x.casesOn (fun _ => Or.inl rfl) fun _ hx => Or.inr <| mem_image_of_mem _ hxâŸ©
#align set.finite_option Set.finite_option

theorem finite_image_fst_and_snd_iff {s : Set (Î± Ã— Î²)} :
    (Prod.fst '' s).Finite âˆ§ (Prod.snd '' s).Finite â†” s.Finite :=
  âŸ¨fun h => (h.1.prod h.2).subset fun _ h => âŸ¨mem_image_of_mem _ h, mem_image_of_mem _ hâŸ©,
    fun h => âŸ¨h.image _, h.image _âŸ©âŸ©
#align set.finite_image_fst_and_snd_iff Set.finite_image_fst_and_snd_iff

theorem forall_finite_image_eval_iff {Î´ : Type*} [Finite Î´] {Îº : Î´ â†’ Type*} {s : Set (âˆ€ d, Îº d)} :
    (âˆ€ d, (eval d '' s).Finite) â†” s.Finite :=
  âŸ¨fun h => (Finite.pi h).subset <| subset_pi_eval_image _ _, fun h _ => h.image _âŸ©
#align set.forall_finite_image_eval_iff Set.forall_finite_image_eval_iff

theorem finite_subset_iUnion {s : Set Î±} (hs : s.Finite) {Î¹} {t : Î¹ â†’ Set Î±} (h : s âŠ† â‹ƒ i, t i) :
    âˆƒ I : Set Î¹, I.Finite âˆ§ s âŠ† â‹ƒ i âˆˆ I, t i := by
  have := hs.to_subtype
  choose f hf using show âˆ€ x : s, âˆƒ i, x.1 âˆˆ t i by simpa [subset_def] using h
  refine âŸ¨range f, finite_range f, fun x hx => ?_âŸ©
  rw [biUnion_range, mem_iUnion]
  exact âŸ¨âŸ¨x, hxâŸ©, hf _âŸ©
#align set.finite_subset_Union Set.finite_subset_iUnion

theorem eq_finite_iUnion_of_finite_subset_iUnion {Î¹} {s : Î¹ â†’ Set Î±} {t : Set Î±} (tfin : t.Finite)
    (h : t âŠ† â‹ƒ i, s i) :
    âˆƒ I : Set Î¹,
      I.Finite âˆ§
        âˆƒ Ïƒ : { i | i âˆˆ I } â†’ Set Î±, (âˆ€ i, (Ïƒ i).Finite) âˆ§ (âˆ€ i, Ïƒ i âŠ† s i) âˆ§ t = â‹ƒ i, Ïƒ i :=
  let âŸ¨I, Ifin, hIâŸ© := finite_subset_iUnion tfin h
  âŸ¨I, Ifin, fun x => s x âˆ© t, fun i => tfin.subset inter_subset_right, fun i =>
    inter_subset_left, by
    ext x
    rw [mem_iUnion]
    constructor
    Â· intro x_in
      rcases mem_iUnion.mp (hI x_in) with âŸ¨i, _, âŸ¨hi, rflâŸ©, HâŸ©
      exact âŸ¨âŸ¨i, hiâŸ©, âŸ¨H, x_inâŸ©âŸ©
    Â· rintro âŸ¨i, -, HâŸ©
      exact HâŸ©
#align set.eq_finite_Union_of_finite_subset_Union Set.eq_finite_iUnion_of_finite_subset_iUnion

@[elab_as_elim]
theorem Finite.induction_on {C : Set Î± â†’ Prop} {s : Set Î±} (h : s.Finite) (H0 : C âˆ…)
    (H1 : âˆ€ {a s}, a âˆ‰ s â†’ Set.Finite s â†’ C s â†’ C (insert a s)) : C s := by
  lift s to Finset Î± using h
  induction' s using Finset.cons_induction_on with a s ha hs
  Â· rwa [Finset.coe_empty]
  Â· rw [Finset.coe_cons]
    exact @H1 a s ha (Set.toFinite _) hs
#align set.finite.induction_on Set.Finite.induction_on

/-- Analogous to `Finset.induction_on'`. -/
@[elab_as_elim]
theorem Finite.induction_on' {C : Set Î± â†’ Prop} {S : Set Î±} (h : S.Finite) (H0 : C âˆ…)
    (H1 : âˆ€ {a s}, a âˆˆ S â†’ s âŠ† S â†’ a âˆ‰ s â†’ C s â†’ C (insert a s)) : C S := by
  refine @Set.Finite.induction_on Î± (fun s => s âŠ† S â†’ C s) S h (fun _ => H0) ?_ Subset.rfl
  intro a s has _ hCs haS
  rw [insert_subset_iff] at haS
  exact H1 haS.1 haS.2 has (hCs haS.2)
#align set.finite.induction_on' Set.Finite.induction_on'

@[elab_as_elim]
theorem Finite.dinduction_on {C : âˆ€ s : Set Î±, s.Finite â†’ Prop} (s : Set Î±) (h : s.Finite)
    (H0 : C âˆ… finite_empty)
    (H1 : âˆ€ {a s}, a âˆ‰ s â†’ âˆ€ h : Set.Finite s, C s h â†’ C (insert a s) (h.insert a)) : C s h :=
  have : âˆ€ h : s.Finite, C s h :=
    Finite.induction_on h (fun _ => H0) fun has hs ih _ => H1 has hs (ih _)
  this h
#align set.finite.dinduction_on Set.Finite.dinduction_on

/-- Induction up to a finite set `S`. -/
theorem Finite.induction_to {C : Set Î± â†’ Prop} {S : Set Î±} (h : S.Finite)
    (S0 : Set Î±) (hS0 : S0 âŠ† S) (H0 : C S0) (H1 : âˆ€ s âŠ‚ S, C s â†’ âˆƒ a âˆˆ S \ s, C (insert a s)) :
    C S := by
  have : Finite S := Finite.to_subtype h
  have : Finite {T : Set Î± // T âŠ† S} := Finite.of_equiv (Set S) (Equiv.Set.powerset S).symm
  rw [â† Subtype.coe_mk (p := (Â· âŠ† S)) _ le_rfl]
  rw [â† Subtype.coe_mk (p := (Â· âŠ† S)) _ hS0] at H0
  refine Finite.to_wellFoundedGT.wf.induction_bot' (fun s hs hs' â†¦ ?_) H0
  obtain âŸ¨a, âŸ¨ha1, ha2âŸ©, ha'âŸ© := H1 s (ssubset_of_ne_of_subset hs s.2) hs'
  exact âŸ¨âŸ¨insert a s.1, insert_subset ha1 s.2âŸ©, Set.ssubset_insert ha2, ha'âŸ©

/-- Induction up to `univ`. -/
theorem Finite.induction_to_univ [Finite Î±] {C : Set Î± â†’ Prop} (S0 : Set Î±)
    (H0 : C S0) (H1 : âˆ€ S â‰  univ, C S â†’ âˆƒ a âˆ‰ S, C (insert a S)) : C univ :=
  finite_univ.induction_to S0 (subset_univ S0) H0 (by simpa [ssubset_univ_iff])

section

attribute [local instance] Nat.fintypeIio

/-- If `P` is some relation between terms of `Î³` and sets in `Î³`, such that every finite set
`t : Set Î³` has some `c : Î³` related to it, then there is a recursively defined sequence `u` in `Î³`
so `u n` is related to the image of `{0, 1, ..., n-1}` under `u`.

(We use this later to show sequentially compact sets are totally bounded.)
-/
theorem seq_of_forall_finite_exists {Î³ : Type*} {P : Î³ â†’ Set Î³ â†’ Prop}
    (h : âˆ€ t : Set Î³, t.Finite â†’ âˆƒ c, P c t) : âˆƒ u : â„• â†’ Î³, âˆ€ n, P (u n) (u '' Iio n) := by
  haveI : Nonempty Î³ := (h âˆ… finite_empty).nonempty
  choose! c hc using h
  set f : (n : â„•) â†’ (g : (m : â„•) â†’ m < n â†’ Î³) â†’ Î³ := fun n g => c (range fun k : Iio n => g k.1 k.2)
  set u : â„• â†’ Î³ := fun n => Nat.strongRecOn' n f
  refine âŸ¨u, fun n => ?_âŸ©
  convert hc (u '' Iio n) ((finite_lt_nat _).image _)
  rw [image_eq_range]
  exact Nat.strongRecOn'_beta
#align set.seq_of_forall_finite_exists Set.seq_of_forall_finite_exists

end

/-! ### Cardinality -/

theorem empty_card : Fintype.card (âˆ… : Set Î±) = 0 :=
  rfl
#align set.empty_card Set.empty_card

theorem empty_card' {h : Fintype.{u} (âˆ… : Set Î±)} : @Fintype.card (âˆ… : Set Î±) h = 0 := by
  simp
#align set.empty_card' Set.empty_card'

theorem card_fintypeInsertOfNotMem {a : Î±} (s : Set Î±) [Fintype s] (h : a âˆ‰ s) :
    @Fintype.card _ (fintypeInsertOfNotMem s h) = Fintype.card s + 1 := by
  simp [fintypeInsertOfNotMem, Fintype.card_ofFinset]
#align set.card_fintype_insert_of_not_mem Set.card_fintypeInsertOfNotMem

@[simp]
theorem card_insert {a : Î±} (s : Set Î±) [Fintype s] (h : a âˆ‰ s)
    {d : Fintype.{u} (insert a s : Set Î±)} : @Fintype.card _ d = Fintype.card s + 1 := by
  rw [â† card_fintypeInsertOfNotMem s h]; congr!
#align set.card_insert Set.card_insert

theorem card_image_of_inj_on {s : Set Î±} [Fintype s] {f : Î± â†’ Î²} [Fintype (f '' s)]
    (H : âˆ€ x âˆˆ s, âˆ€ y âˆˆ s, f x = f y â†’ x = y) : Fintype.card (f '' s) = Fintype.card s :=
  haveI := Classical.propDecidable
  calc
    Fintype.card (f '' s) = (s.toFinset.image f).card := Fintype.card_of_finset' _ (by simp)
    _ = s.toFinset.card :=
      Finset.card_image_of_injOn fun x hx y hy hxy =>
        H x (mem_toFinset.1 hx) y (mem_toFinset.1 hy) hxy
    _ = Fintype.card s := (Fintype.card_of_finset' _ fun a => mem_toFinset).symm
#align set.card_image_of_inj_on Set.card_image_of_inj_on

theorem card_image_of_injective (s : Set Î±) [Fintype s] {f : Î± â†’ Î²} [Fintype (f '' s)]
    (H : Function.Injective f) : Fintype.card (f '' s) = Fintype.card s :=
  card_image_of_inj_on fun _ _ _ _ h => H h
#align set.card_image_of_injective Set.card_image_of_injective

@[simp]
theorem card_singleton (a : Î±) : Fintype.card ({a} : Set Î±) = 1 :=
  Fintype.card_ofSubsingleton _
#align set.card_singleton Set.card_singleton

theorem card_lt_card {s t : Set Î±} [Fintype s] [Fintype t] (h : s âŠ‚ t) :
    Fintype.card s < Fintype.card t :=
  Fintype.card_lt_of_injective_not_surjective (Set.inclusion h.1) (Set.inclusion_injective h.1)
    fun hst => (ssubset_iff_subset_ne.1 h).2 (eq_of_inclusion_surjective hst)
#align set.card_lt_card Set.card_lt_card

theorem card_le_card {s t : Set Î±} [Fintype s] [Fintype t] (hsub : s âŠ† t) :
    Fintype.card s â‰¤ Fintype.card t :=
  Fintype.card_le_of_injective (Set.inclusion hsub) (Set.inclusion_injective hsub)
#align set.card_le_card Set.card_le_card

theorem eq_of_subset_of_card_le {s t : Set Î±} [Fintype s] [Fintype t] (hsub : s âŠ† t)
    (hcard : Fintype.card t â‰¤ Fintype.card s) : s = t :=
  (eq_or_ssubset_of_subset hsub).elim id fun h => absurd hcard <| not_le_of_lt <| card_lt_card h
#align set.eq_of_subset_of_card_le Set.eq_of_subset_of_card_le

theorem card_range_of_injective [Fintype Î±] {f : Î± â†’ Î²} (hf : Injective f) [Fintype (range f)] :
    Fintype.card (range f) = Fintype.card Î± :=
  Eq.symm <| Fintype.card_congr <| Equiv.ofInjective f hf
#align set.card_range_of_injective Set.card_range_of_injective

theorem Finite.card_toFinset {s : Set Î±} [Fintype s] (h : s.Finite) :
    h.toFinset.card = Fintype.card s :=
  Eq.symm <| Fintype.card_of_finset' _ fun _ â†¦ h.mem_toFinset
#align set.finite.card_to_finset Set.Finite.card_toFinset

theorem card_ne_eq [Fintype Î±] (a : Î±) [Fintype { x : Î± | x â‰  a }] :
    Fintype.card { x : Î± | x â‰  a } = Fintype.card Î± - 1 := by
  haveI := Classical.decEq Î±
  rw [â† toFinset_card, toFinset_setOf, Finset.filter_ne',
    Finset.card_erase_of_mem (Finset.mem_univ _), Finset.card_univ]
#align set.card_ne_eq Set.card_ne_eq

/-! ### Infinite sets -/

variable {s t : Set Î±}

theorem infinite_univ_iff : (@univ Î±).Infinite â†” Infinite Î± := by
  rw [Set.Infinite, finite_univ_iff, not_finite_iff_infinite]
#align set.infinite_univ_iff Set.infinite_univ_iff

theorem infinite_univ [h : Infinite Î±] : (@univ Î±).Infinite :=
  infinite_univ_iff.2 h
#align set.infinite_univ Set.infinite_univ

theorem infinite_coe_iff {s : Set Î±} : Infinite s â†” s.Infinite :=
  not_finite_iff_infinite.symm.trans finite_coe_iff.not
#align set.infinite_coe_iff Set.infinite_coe_iff

-- Porting note: something weird happened here
alias âŸ¨_, Infinite.to_subtypeâŸ© := infinite_coe_iff
#align set.infinite.to_subtype Set.Infinite.to_subtype

lemma Infinite.exists_not_mem_finite (hs : s.Infinite) (ht : t.Finite) : âˆƒ a, a âˆˆ s âˆ§ a âˆ‰ t := by
  by_contra! h; exact hs <| ht.subset h

lemma Infinite.exists_not_mem_finset (hs : s.Infinite) (t : Finset Î±) : âˆƒ a âˆˆ s, a âˆ‰ t :=
  hs.exists_not_mem_finite t.finite_toSet
#align set.infinite.exists_not_mem_finset Set.Infinite.exists_not_mem_finset

section Infinite
variable [Infinite Î±]

lemma Finite.exists_not_mem (hs : s.Finite) : âˆƒ a, a âˆ‰ s := by
  by_contra! h; exact infinite_univ (hs.subset fun a _ â†¦ h _)

lemma _root_.Finset.exists_not_mem (s : Finset Î±) : âˆƒ a, a âˆ‰ s := s.finite_toSet.exists_not_mem

end Infinite

/-- Embedding of `â„•` into an infinite set. -/
noncomputable def Infinite.natEmbedding (s : Set Î±) (h : s.Infinite) : â„• â†ª s :=
  h.to_subtype.natEmbedding
#align set.infinite.nat_embedding Set.Infinite.natEmbedding

theorem Infinite.exists_subset_card_eq {s : Set Î±} (hs : s.Infinite) (n : â„•) :
    âˆƒ t : Finset Î±, â†‘t âŠ† s âˆ§ t.card = n :=
  âŸ¨((Finset.range n).map (hs.natEmbedding _)).map (Embedding.subtype _), by simpâŸ©
#align set.infinite.exists_subset_card_eq Set.Infinite.exists_subset_card_eq

theorem infinite_of_finite_compl [Infinite Î±] {s : Set Î±} (hs : sá¶œ.Finite) : s.Infinite := fun h =>
  Set.infinite_univ (by simpa using hs.union h)
#align set.infinite_of_finite_compl Set.infinite_of_finite_compl

theorem Finite.infinite_compl [Infinite Î±] {s : Set Î±} (hs : s.Finite) : sá¶œ.Infinite := fun h =>
  Set.infinite_univ (by simpa using hs.union h)
#align set.finite.infinite_compl Set.Finite.infinite_compl

theorem Infinite.diff {s t : Set Î±} (hs : s.Infinite) (ht : t.Finite) : (s \ t).Infinite := fun h =>
  hs <| h.of_diff ht
#align set.infinite.diff Set.Infinite.diff

@[simp]
theorem infinite_union {s t : Set Î±} : (s âˆª t).Infinite â†” s.Infinite âˆ¨ t.Infinite := by
  simp only [Set.Infinite, finite_union, not_and_or]
#align set.infinite_union Set.infinite_union

theorem Infinite.of_image (f : Î± â†’ Î²) {s : Set Î±} (hs : (f '' s).Infinite) : s.Infinite :=
  mt (Finite.image f) hs
#align set.infinite.of_image Set.Infinite.of_image

theorem infinite_image_iff {s : Set Î±} {f : Î± â†’ Î²} (hi : InjOn f s) :
    (f '' s).Infinite â†” s.Infinite :=
  not_congr <| finite_image_iff hi
#align set.infinite_image_iff Set.infinite_image_iff

theorem infinite_range_iff {f : Î± â†’ Î²} (hi : Injective f) :
    (range f).Infinite â†” Infinite Î± := by
  rw [â† image_univ, infinite_image_iff hi.injOn, infinite_univ_iff]

alias âŸ¨_, Infinite.imageâŸ© := infinite_image_iff
#align set.infinite.image Set.Infinite.image

-- Porting note: attribute [protected] doesn't work
-- attribute [protected] infinite.image

section Image2

variable {f : Î± â†’ Î² â†’ Î³} {s : Set Î±} {t : Set Î²} {a : Î±} {b : Î²}

protected theorem Infinite.image2_left (hs : s.Infinite) (hb : b âˆˆ t)
    (hf : InjOn (fun a => f a b) s) : (image2 f s t).Infinite :=
  (hs.image hf).mono <| image_subset_image2_left hb
#align set.infinite.image2_left Set.Infinite.image2_left

protected theorem Infinite.image2_right (ht : t.Infinite) (ha : a âˆˆ s) (hf : InjOn (f a) t) :
    (image2 f s t).Infinite :=
  (ht.image hf).mono <| image_subset_image2_right ha
#align set.infinite.image2_right Set.Infinite.image2_right

theorem infinite_image2 (hfs : âˆ€ b âˆˆ t, InjOn (fun a => f a b) s) (hft : âˆ€ a âˆˆ s, InjOn (f a) t) :
    (image2 f s t).Infinite â†” s.Infinite âˆ§ t.Nonempty âˆ¨ t.Infinite âˆ§ s.Nonempty := by
  refine âŸ¨fun h => Set.infinite_prod.1 ?_, ?_âŸ©
  Â· rw [â† image_uncurry_prod] at h
    exact h.of_image _
  Â· rintro (âŸ¨hs, b, hbâŸ© | âŸ¨ht, a, haâŸ©)
    Â· exact hs.image2_left hb (hfs _ hb)
    Â· exact ht.image2_right ha (hft _ ha)
#align set.infinite_image2 Set.infinite_image2

lemma finite_image2 (hfs : âˆ€ b âˆˆ t, InjOn (f Â· b) s) (hft : âˆ€ a âˆˆ s, InjOn (f a) t) :
    (image2 f s t).Finite â†” s.Finite âˆ§ t.Finite âˆ¨ s = âˆ… âˆ¨ t = âˆ… := by
  rw [â† not_infinite, infinite_image2 hfs hft]
  simp [not_or, -not_and, not_and_or, not_nonempty_iff_eq_empty]
  aesop

end Image2

theorem infinite_of_injOn_mapsTo {s : Set Î±} {t : Set Î²} {f : Î± â†’ Î²} (hi : InjOn f s)
    (hm : MapsTo f s t) (hs : s.Infinite) : t.Infinite :=
  ((infinite_image_iff hi).2 hs).mono (mapsTo'.mp hm)
#align set.infinite_of_inj_on_maps_to Set.infinite_of_injOn_mapsTo

theorem Infinite.exists_ne_map_eq_of_mapsTo {s : Set Î±} {t : Set Î²} {f : Î± â†’ Î²} (hs : s.Infinite)
    (hf : MapsTo f s t) (ht : t.Finite) : âˆƒ x âˆˆ s, âˆƒ y âˆˆ s, x â‰  y âˆ§ f x = f y := by
  contrapose! ht
  exact infinite_of_injOn_mapsTo (fun x hx y hy => not_imp_not.1 (ht x hx y hy)) hf hs
#align set.infinite.exists_ne_map_eq_of_maps_to Set.Infinite.exists_ne_map_eq_of_mapsTo

theorem infinite_range_of_injective [Infinite Î±] {f : Î± â†’ Î²} (hi : Injective f) :
    (range f).Infinite := by
  rw [â† image_univ, infinite_image_iff hi.injOn]
  exact infinite_univ
#align set.infinite_range_of_injective Set.infinite_range_of_injective

theorem infinite_of_injective_forall_mem [Infinite Î±] {s : Set Î²} {f : Î± â†’ Î²} (hi : Injective f)
    (hf : âˆ€ x : Î±, f x âˆˆ s) : s.Infinite := by
  rw [â† range_subset_iff] at hf
  exact (infinite_range_of_injective hi).mono hf
#align set.infinite_of_injective_forall_mem Set.infinite_of_injective_forall_mem

theorem not_injOn_infinite_finite_image {f : Î± â†’ Î²} {s : Set Î±} (h_inf : s.Infinite)
    (h_fin : (f '' s).Finite) : Â¬InjOn f s := by
  have : Finite (f '' s) := finite_coe_iff.mpr h_fin
  have : Infinite s := infinite_coe_iff.mpr h_inf
  have h := not_injective_infinite_finite
            ((f '' s).codRestrict (s.restrict f) fun x => âŸ¨x, x.property, rflâŸ©)
  contrapose! h
  rwa [injective_codRestrict, â† injOn_iff_injective]
#align set.not_inj_on_infinite_finite_image Set.not_injOn_infinite_finite_image

/-! ### Order properties -/

section Preorder

variable [Preorder Î±] [Nonempty Î±] {s : Set Î±}

theorem infinite_of_forall_exists_gt (h : âˆ€ a, âˆƒ b âˆˆ s, a < b) : s.Infinite := by
  inhabit Î±
  set f : â„• â†’ Î± := fun n => Nat.recOn n (h default).choose fun _ a => (h a).choose
  have hf : âˆ€ n, f n âˆˆ s := by rintro (_ | _) <;> exact (h _).choose_spec.1
  exact infinite_of_injective_forall_mem
    (strictMono_nat_of_lt_succ fun n => (h _).choose_spec.2).injective hf
#align set.infinite_of_forall_exists_gt Set.infinite_of_forall_exists_gt

theorem infinite_of_forall_exists_lt (h : âˆ€ a, âˆƒ b âˆˆ s, b < a) : s.Infinite :=
  @infinite_of_forall_exists_gt Î±áµ’áµˆ _ _ _ h
#align set.infinite_of_forall_exists_lt Set.infinite_of_forall_exists_lt

end Preorder

theorem finite_isTop (Î± : Type*) [PartialOrder Î±] : { x : Î± | IsTop x }.Finite :=
  (subsingleton_isTop Î±).finite
#align set.finite_is_top Set.finite_isTop

theorem finite_isBot (Î± : Type*) [PartialOrder Î±] : { x : Î± | IsBot x }.Finite :=
  (subsingleton_isBot Î±).finite
#align set.finite_is_bot Set.finite_isBot

theorem Infinite.exists_lt_map_eq_of_mapsTo [LinearOrder Î±] {s : Set Î±} {t : Set Î²} {f : Î± â†’ Î²}
    (hs : s.Infinite) (hf : MapsTo f s t) (ht : t.Finite) : âˆƒ x âˆˆ s, âˆƒ y âˆˆ s, x < y âˆ§ f x = f y :=
  let âŸ¨x, hx, y, hy, hxy, hfâŸ© := hs.exists_ne_map_eq_of_mapsTo hf ht
  hxy.lt_or_lt.elim (fun hxy => âŸ¨x, hx, y, hy, hxy, hfâŸ©) fun hyx => âŸ¨y, hy, x, hx, hyx, hf.symmâŸ©
#align set.infinite.exists_lt_map_eq_of_maps_to Set.Infinite.exists_lt_map_eq_of_mapsTo

theorem Finite.exists_lt_map_eq_of_forall_mem [LinearOrder Î±] [Infinite Î±] {t : Set Î²} {f : Î± â†’ Î²}
    (hf : âˆ€ a, f a âˆˆ t) (ht : t.Finite) : âˆƒ a b, a < b âˆ§ f a = f b := by
  rw [â† mapsTo_univ_iff] at hf
  obtain âŸ¨a, -, b, -, hâŸ© := infinite_univ.exists_lt_map_eq_of_mapsTo hf ht
  exact âŸ¨a, b, hâŸ©
#align set.finite.exists_lt_map_eq_of_forall_mem Set.Finite.exists_lt_map_eq_of_forall_mem

theorem exists_min_image [LinearOrder Î²] (s : Set Î±) (f : Î± â†’ Î²) (h1 : s.Finite) :
    s.Nonempty â†’ âˆƒ a âˆˆ s, âˆ€ b âˆˆ s, f a â‰¤ f b
  | âŸ¨x, hxâŸ© => by
    simpa only [exists_prop, Finite.mem_toFinset] using
      h1.toFinset.exists_min_image f âŸ¨x, h1.mem_toFinset.2 hxâŸ©
#align set.exists_min_image Set.exists_min_image

theorem exists_max_image [LinearOrder Î²] (s : Set Î±) (f : Î± â†’ Î²) (h1 : s.Finite) :
    s.Nonempty â†’ âˆƒ a âˆˆ s, âˆ€ b âˆˆ s, f b â‰¤ f a
  | âŸ¨x, hxâŸ© => by
    simpa only [exists_prop, Finite.mem_toFinset] using
      h1.toFinset.exists_max_image f âŸ¨x, h1.mem_toFinset.2 hxâŸ©
#align set.exists_max_image Set.exists_max_image

theorem exists_lower_bound_image [Nonempty Î±] [LinearOrder Î²] (s : Set Î±) (f : Î± â†’ Î²)
    (h : s.Finite) : âˆƒ a : Î±, âˆ€ b âˆˆ s, f a â‰¤ f b := by
  rcases s.eq_empty_or_nonempty with rfl | hs
  Â· exact â€¹Nonempty Î±â€º.elim fun a => âŸ¨a, fun _ => False.elimâŸ©
  Â· rcases Set.exists_min_image s f h hs with âŸ¨xâ‚€, _, hxâ‚€âŸ©
    exact âŸ¨xâ‚€, fun x hx => hxâ‚€ x hxâŸ©
#align set.exists_lower_bound_image Set.exists_lower_bound_image

theorem exists_upper_bound_image [Nonempty Î±] [LinearOrder Î²] (s : Set Î±) (f : Î± â†’ Î²)
    (h : s.Finite) : âˆƒ a : Î±, âˆ€ b âˆˆ s, f b â‰¤ f a :=
  exists_lower_bound_image (Î² := Î²áµ’áµˆ) s f h
#align set.exists_upper_bound_image Set.exists_upper_bound_image

theorem Finite.iSup_biInf_of_monotone {Î¹ Î¹' Î± : Type*} [Preorder Î¹'] [Nonempty Î¹']
    [IsDirected Î¹' (Â· â‰¤ Â·)] [Order.Frame Î±] {s : Set Î¹} (hs : s.Finite) {f : Î¹ â†’ Î¹' â†’ Î±}
    (hf : âˆ€ i âˆˆ s, Monotone (f i)) : â¨† j, â¨… i âˆˆ s, f i j = â¨… i âˆˆ s, â¨† j, f i j := by
  induction' s, hs using Set.Finite.dinduction_on with a s _ _ ihs hf
  Â· simp [iSup_const]
  Â· rw [forall_mem_insert] at hf
    simp only [iInf_insert, â† ihs hf.2]
    exact iSup_inf_of_monotone hf.1 fun jâ‚ jâ‚‚ hj => iInfâ‚‚_mono fun i hi => hf.2 i hi hj
#align set.finite.supr_binfi_of_monotone Set.Finite.iSup_biInf_of_monotone

theorem Finite.iSup_biInf_of_antitone {Î¹ Î¹' Î± : Type*} [Preorder Î¹'] [Nonempty Î¹']
    [IsDirected Î¹' (swap (Â· â‰¤ Â·))] [Order.Frame Î±] {s : Set Î¹} (hs : s.Finite) {f : Î¹ â†’ Î¹' â†’ Î±}
    (hf : âˆ€ i âˆˆ s, Antitone (f i)) : â¨† j, â¨… i âˆˆ s, f i j = â¨… i âˆˆ s, â¨† j, f i j :=
  @Finite.iSup_biInf_of_monotone Î¹ Î¹'áµ’áµˆ Î± _ _ _ _ _ hs _ fun i hi => (hf i hi).dual_left
#align set.finite.supr_binfi_of_antitone Set.Finite.iSup_biInf_of_antitone

theorem Finite.iInf_biSup_of_monotone {Î¹ Î¹' Î± : Type*} [Preorder Î¹'] [Nonempty Î¹']
    [IsDirected Î¹' (swap (Â· â‰¤ Â·))] [Order.Coframe Î±] {s : Set Î¹} (hs : s.Finite) {f : Î¹ â†’ Î¹' â†’ Î±}
    (hf : âˆ€ i âˆˆ s, Monotone (f i)) : â¨… j, â¨† i âˆˆ s, f i j = â¨† i âˆˆ s, â¨… j, f i j :=
  hs.iSup_biInf_of_antitone (Î± := Î±áµ’áµˆ) fun i hi => (hf i hi).dual_right
#align set.finite.infi_bsupr_of_monotone Set.Finite.iInf_biSup_of_monotone

theorem Finite.iInf_biSup_of_antitone {Î¹ Î¹' Î± : Type*} [Preorder Î¹'] [Nonempty Î¹']
    [IsDirected Î¹' (Â· â‰¤ Â·)] [Order.Coframe Î±] {s : Set Î¹} (hs : s.Finite) {f : Î¹ â†’ Î¹' â†’ Î±}
    (hf : âˆ€ i âˆˆ s, Antitone (f i)) : â¨… j, â¨† i âˆˆ s, f i j = â¨† i âˆˆ s, â¨… j, f i j :=
  hs.iSup_biInf_of_monotone (Î± := Î±áµ’áµˆ) fun i hi => (hf i hi).dual_right
#align set.finite.infi_bsupr_of_antitone Set.Finite.iInf_biSup_of_antitone

theorem iSup_iInf_of_monotone {Î¹ Î¹' Î± : Type*} [Finite Î¹] [Preorder Î¹'] [Nonempty Î¹']
    [IsDirected Î¹' (Â· â‰¤ Â·)] [Order.Frame Î±] {f : Î¹ â†’ Î¹' â†’ Î±} (hf : âˆ€ i, Monotone (f i)) :
    â¨† j, â¨… i, f i j = â¨… i, â¨† j, f i j := by
  simpa only [iInf_univ] using finite_univ.iSup_biInf_of_monotone fun i _ => hf i
#align supr_infi_of_monotone Set.iSup_iInf_of_monotone

theorem iSup_iInf_of_antitone {Î¹ Î¹' Î± : Type*} [Finite Î¹] [Preorder Î¹'] [Nonempty Î¹']
    [IsDirected Î¹' (swap (Â· â‰¤ Â·))] [Order.Frame Î±] {f : Î¹ â†’ Î¹' â†’ Î±} (hf : âˆ€ i, Antitone (f i)) :
    â¨† j, â¨… i, f i j = â¨… i, â¨† j, f i j :=
  @iSup_iInf_of_monotone Î¹ Î¹'áµ’áµˆ Î± _ _ _ _ _ _ fun i => (hf i).dual_left
#align supr_infi_of_antitone Set.iSup_iInf_of_antitone

theorem iInf_iSup_of_monotone {Î¹ Î¹' Î± : Type*} [Finite Î¹] [Preorder Î¹'] [Nonempty Î¹']
    [IsDirected Î¹' (swap (Â· â‰¤ Â·))] [Order.Coframe Î±] {f : Î¹ â†’ Î¹' â†’ Î±} (hf : âˆ€ i, Monotone (f i)) :
    â¨… j, â¨† i, f i j = â¨† i, â¨… j, f i j :=
  iSup_iInf_of_antitone (Î± := Î±áµ’áµˆ) fun i => (hf i).dual_right
#align infi_supr_of_monotone Set.iInf_iSup_of_monotone

theorem iInf_iSup_of_antitone {Î¹ Î¹' Î± : Type*} [Finite Î¹] [Preorder Î¹'] [Nonempty Î¹']
    [IsDirected Î¹' (Â· â‰¤ Â·)] [Order.Coframe Î±] {f : Î¹ â†’ Î¹' â†’ Î±} (hf : âˆ€ i, Antitone (f i)) :
    â¨… j, â¨† i, f i j = â¨† i, â¨… j, f i j :=
  iSup_iInf_of_monotone (Î± := Î±áµ’áµˆ) fun i => (hf i).dual_right
#align infi_supr_of_antitone Set.iInf_iSup_of_antitone

/-- An increasing union distributes over finite intersection. -/
theorem iUnion_iInter_of_monotone {Î¹ Î¹' Î± : Type*} [Finite Î¹] [Preorder Î¹'] [IsDirected Î¹' (Â· â‰¤ Â·)]
    [Nonempty Î¹'] {s : Î¹ â†’ Î¹' â†’ Set Î±} (hs : âˆ€ i, Monotone (s i)) :
    â‹ƒ j : Î¹', â‹‚ i : Î¹, s i j = â‹‚ i : Î¹, â‹ƒ j : Î¹', s i j :=
  iSup_iInf_of_monotone hs
#align set.Union_Inter_of_monotone Set.iUnion_iInter_of_monotone

/-- A decreasing union distributes over finite intersection. -/
theorem iUnion_iInter_of_antitone {Î¹ Î¹' Î± : Type*} [Finite Î¹] [Preorder Î¹']
    [IsDirected Î¹' (swap (Â· â‰¤ Â·))] [Nonempty Î¹'] {s : Î¹ â†’ Î¹' â†’ Set Î±} (hs : âˆ€ i, Antitone (s i)) :
    â‹ƒ j : Î¹', â‹‚ i : Î¹, s i j = â‹‚ i : Î¹, â‹ƒ j : Î¹', s i j :=
  iSup_iInf_of_antitone hs
#align set.Union_Inter_of_antitone Set.iUnion_iInter_of_antitone

/-- An increasing intersection distributes over finite union. -/
theorem iInter_iUnion_of_monotone {Î¹ Î¹' Î± : Type*} [Finite Î¹] [Preorder Î¹']
    [IsDirected Î¹' (swap (Â· â‰¤ Â·))] [Nonempty Î¹'] {s : Î¹ â†’ Î¹' â†’ Set Î±} (hs : âˆ€ i, Monotone (s i)) :
    â‹‚ j : Î¹', â‹ƒ i : Î¹, s i j = â‹ƒ i : Î¹, â‹‚ j : Î¹', s i j :=
  iInf_iSup_of_monotone hs
#align set.Inter_Union_of_monotone Set.iInter_iUnion_of_monotone

/-- A decreasing intersection distributes over finite union. -/
theorem iInter_iUnion_of_antitone {Î¹ Î¹' Î± : Type*} [Finite Î¹] [Preorder Î¹'] [IsDirected Î¹' (Â· â‰¤ Â·)]
    [Nonempty Î¹'] {s : Î¹ â†’ Î¹' â†’ Set Î±} (hs : âˆ€ i, Antitone (s i)) :
    â‹‚ j : Î¹', â‹ƒ i : Î¹, s i j = â‹ƒ i : Î¹, â‹‚ j : Î¹', s i j :=
  iInf_iSup_of_antitone hs
#align set.Inter_Union_of_antitone Set.iInter_iUnion_of_antitone

theorem iUnion_pi_of_monotone {Î¹ Î¹' : Type*} [LinearOrder Î¹'] [Nonempty Î¹'] {Î± : Î¹ â†’ Type*}
    {I : Set Î¹} {s : âˆ€ i, Î¹' â†’ Set (Î± i)} (hI : I.Finite) (hs : âˆ€ i âˆˆ I, Monotone (s i)) :
    â‹ƒ j : Î¹', I.pi (fun i => s i j) = I.pi fun i => â‹ƒ j, s i j := by
  simp only [pi_def, biInter_eq_iInter, preimage_iUnion]
  haveI := hI.fintype.finite
  refine iUnion_iInter_of_monotone (Î¹' := Î¹') (fun (i : I) jâ‚ jâ‚‚ h => ?_)
  exact preimage_mono <| hs i i.2 h
#align set.Union_pi_of_monotone Set.iUnion_pi_of_monotone

theorem iUnion_univ_pi_of_monotone {Î¹ Î¹' : Type*} [LinearOrder Î¹'] [Nonempty Î¹'] [Finite Î¹]
    {Î± : Î¹ â†’ Type*} {s : âˆ€ i, Î¹' â†’ Set (Î± i)} (hs : âˆ€ i, Monotone (s i)) :
    â‹ƒ j : Î¹', pi univ (fun i => s i j) = pi univ fun i => â‹ƒ j, s i j :=
  iUnion_pi_of_monotone finite_univ fun i _ => hs i
#align set.Union_univ_pi_of_monotone Set.iUnion_univ_pi_of_monotone

theorem finite_range_findGreatest {P : Î± â†’ â„• â†’ Prop} [âˆ€ x, DecidablePred (P x)] {b : â„•} :
    (range fun x => Nat.findGreatest (P x) b).Finite :=
  (finite_le_nat b).subset <| range_subset_iff.2 fun _ => Nat.findGreatest_le _
#align set.finite_range_find_greatest Set.finite_range_findGreatest

theorem Finite.exists_maximal_wrt [PartialOrder Î²] (f : Î± â†’ Î²) (s : Set Î±) (h : s.Finite)
    (hs : s.Nonempty) : âˆƒ a âˆˆ s, âˆ€ a' âˆˆ s, f a â‰¤ f a' â†’ f a = f a' := by
  induction s, h using Set.Finite.dinduction_on with
  | H0 => exact absurd hs not_nonempty_empty
  | @H1 a s his _ ih =>
    rcases s.eq_empty_or_nonempty with h | h
    Â· use a
      simp [h]
    rcases ih h with âŸ¨b, hb, ihâŸ©
    by_cases h : f b â‰¤ f a
    Â· refine âŸ¨a, Set.mem_insert _ _, fun c hc hac => le_antisymm hac ?_âŸ©
      rcases Set.mem_insert_iff.1 hc with (rfl | hcs)
      Â· rfl
      Â· rwa [â† ih c hcs (le_trans h hac)]
    Â· refine âŸ¨b, Set.mem_insert_of_mem _ hb, fun c hc hbc => ?_âŸ©
      rcases Set.mem_insert_iff.1 hc with (rfl | hcs)
      Â· exact (h hbc).elim
      Â· exact ih c hcs hbc
#align set.finite.exists_maximal_wrt Set.Finite.exists_maximal_wrt

/-- A version of `Finite.exists_maximal_wrt` with the (weaker) hypothesis that the image of `s`
  is finite rather than `s` itself. -/
theorem Finite.exists_maximal_wrt' [PartialOrder Î²] (f : Î± â†’ Î²) (s : Set Î±) (h : (f '' s).Finite)
    (hs : s.Nonempty) : (âˆƒ a âˆˆ s, âˆ€ (a' : Î±), a' âˆˆ s â†’ f a â‰¤ f a' â†’ f a = f a') := by
  obtain âŸ¨_, âŸ¨a, ha, rflâŸ©, hmaxâŸ© := Finite.exists_maximal_wrt id (f '' s) h (hs.image f)
  exact âŸ¨a, ha, fun a' ha' hf â†¦ hmax _ (mem_image_of_mem f ha') hfâŸ©

theorem Finite.exists_minimal_wrt [PartialOrder Î²] (f : Î± â†’ Î²) (s : Set Î±) (h : s.Finite)
    (hs : s.Nonempty) : âˆƒ a âˆˆ s, âˆ€ a' âˆˆ s, f a' â‰¤ f a â†’ f a = f a' :=
  Finite.exists_maximal_wrt (Î² := Î²áµ’áµˆ) f s h hs

/-- A version of `Finite.exists_minimal_wrt` with the (weaker) hypothesis that the image of `s`
  is finite rather than `s` itself. -/
lemma Finite.exists_minimal_wrt' [PartialOrder Î²] (f : Î± â†’ Î²) (s : Set Î±) (h : (f '' s).Finite)
    (hs : s.Nonempty) : (âˆƒ a âˆˆ s, âˆ€ (a' : Î±), a' âˆˆ s â†’ f a' â‰¤ f a â†’ f a = f a') :=
  Set.Finite.exists_maximal_wrt' (Î² := Î²áµ’áµˆ) f s h hs

section

variable [Preorder Î±] [IsDirected Î± (Â· â‰¤ Â·)] [Nonempty Î±] {s : Set Î±}

/-- A finite set is bounded above. -/
protected theorem Finite.bddAbove (hs : s.Finite) : BddAbove s :=
  Finite.induction_on hs bddAbove_empty fun _ _ h => h.insert _
#align set.finite.bdd_above Set.Finite.bddAbove

/-- A finite union of sets which are all bounded above is still bounded above. -/
theorem Finite.bddAbove_biUnion {I : Set Î²} {S : Î² â†’ Set Î±} (H : I.Finite) :
    BddAbove (â‹ƒ i âˆˆ I, S i) â†” âˆ€ i âˆˆ I, BddAbove (S i) :=
  Finite.induction_on H (by simp only [biUnion_empty, bddAbove_empty, forall_mem_empty])
    fun _ _ hs => by simp only [biUnion_insert, forall_mem_insert, bddAbove_union, hs]
#align set.finite.bdd_above_bUnion Set.Finite.bddAbove_biUnion

theorem infinite_of_not_bddAbove : Â¬BddAbove s â†’ s.Infinite :=
  mt Finite.bddAbove
#align set.infinite_of_not_bdd_above Set.infinite_of_not_bddAbove

end

section

variable [Preorder Î±] [IsDirected Î± (Â· â‰¥ Â·)] [Nonempty Î±] {s : Set Î±}

/-- A finite set is bounded below. -/
protected theorem Finite.bddBelow (hs : s.Finite) : BddBelow s :=
  @Finite.bddAbove Î±áµ’áµˆ _ _ _ _ hs
#align set.finite.bdd_below Set.Finite.bddBelow

/-- A finite union of sets which are all bounded below is still bounded below. -/
theorem Finite.bddBelow_biUnion {I : Set Î²} {S : Î² â†’ Set Î±} (H : I.Finite) :
    BddBelow (â‹ƒ i âˆˆ I, S i) â†” âˆ€ i âˆˆ I, BddBelow (S i) :=
  @Finite.bddAbove_biUnion Î±áµ’áµˆ _ _ _ _ _ _ H
#align set.finite.bdd_below_bUnion Set.Finite.bddBelow_biUnion

theorem infinite_of_not_bddBelow : Â¬BddBelow s â†’ s.Infinite := mt Finite.bddBelow
#align set.infinite_of_not_bdd_below Set.infinite_of_not_bddBelow

end

end Set

namespace Finset

lemma exists_card_eq [Infinite Î±] : âˆ€ n : â„•, âˆƒ s : Finset Î±, s.card = n
  | 0 => âŸ¨âˆ…, card_emptyâŸ©
  | n + 1 => by
    classical
    obtain âŸ¨s, rflâŸ© := exists_card_eq n
    obtain âŸ¨a, haâŸ© := s.exists_not_mem
    exact âŸ¨insert a s, card_insert_of_not_mem haâŸ©

/-- A finset is bounded above. -/
protected theorem bddAbove [SemilatticeSup Î±] [Nonempty Î±] (s : Finset Î±) : BddAbove (â†‘s : Set Î±) :=
  s.finite_toSet.bddAbove
#align finset.bdd_above Finset.bddAbove

/-- A finset is bounded below. -/
protected theorem bddBelow [SemilatticeInf Î±] [Nonempty Î±] (s : Finset Î±) : BddBelow (â†‘s : Set Î±) :=
  s.finite_toSet.bddBelow
#align finset.bdd_below Finset.bddBelow

end Finset

variable [LinearOrder Î±] {s : Set Î±}

/-- If a linear order does not contain any triple of elements `x < y < z`, then this type
is finite. -/
lemma Finite.of_forall_not_lt_lt (h : âˆ€ â¦ƒx y z : Î±â¦„, x < y â†’ y < z â†’ False) : Finite Î± := by
  nontriviality Î±
  rcases exists_pair_ne Î± with âŸ¨x, y, hneâŸ©
  refine @Finite.of_fintype Î± âŸ¨{x, y}, fun z => ?_âŸ©
  simpa [hne] using eq_or_eq_or_eq_of_forall_not_lt_lt h z x y
#align finite.of_forall_not_lt_lt Finite.of_forall_not_lt_lt

/-- If a set `s` does not contain any triple of elements `x < y < z`, then `s` is finite. -/
lemma Set.finite_of_forall_not_lt_lt (h : âˆ€ x âˆˆ s, âˆ€ y âˆˆ s, âˆ€ z âˆˆ s, x < y â†’ y < z â†’ False) :
    Set.Finite s :=
  @Set.toFinite _ s <| Finite.of_forall_not_lt_lt <| by simpa only [SetCoe.forall'] using h
#align set.finite_of_forall_not_lt_lt Set.finite_of_forall_not_lt_lt

lemma Set.finite_diff_iUnion_Ioo (s : Set Î±) : (s \ â‹ƒ (x âˆˆ s) (y âˆˆ s), Ioo x y).Finite :=
  Set.finite_of_forall_not_lt_lt fun _x hx _y hy _z hz hxy hyz => hy.2 <| mem_iUnionâ‚‚_of_mem hx.1 <|
    mem_iUnionâ‚‚_of_mem hz.1 âŸ¨hxy, hyzâŸ©
#align set.finite_diff_Union_Ioo Set.finite_diff_iUnion_Ioo

lemma Set.finite_diff_iUnion_Ioo' (s : Set Î±) : (s \ â‹ƒ x : s Ã— s, Ioo x.1 x.2).Finite := by
  simpa only [iUnion, iSup_prod, iSup_subtype] using s.finite_diff_iUnion_Ioo
#align set.finite_diff_Union_Ioo' Set.finite_diff_iUnion_Ioo'

lemma Directed.exists_mem_subset_of_finset_subset_biUnion {Î± Î¹ : Type*} [Nonempty Î¹]
    {f : Î¹ â†’ Set Î±} (h : Directed (Â· âŠ† Â·) f) {s : Finset Î±} (hs : (s : Set Î±) âŠ† â‹ƒ i, f i) :
    âˆƒ i, (s : Set Î±) âŠ† f i := by
  induction s using Finset.cons_induction with
  | empty => simp
  | cons b t hbt iht =>
    simp only [Finset.coe_cons, Set.insert_subset_iff, Set.mem_iUnion] at hs âŠ¢
    rcases hs.imp_right iht with âŸ¨âŸ¨i, hiâŸ©, j, hjâŸ©
    rcases h i j with âŸ¨k, hik, hjkâŸ©
    exact âŸ¨k, hik hi, hj.trans hjkâŸ©
#align directed.exists_mem_subset_of_finset_subset_bUnion Directed.exists_mem_subset_of_finset_subset_biUnion

theorem DirectedOn.exists_mem_subset_of_finset_subset_biUnion {Î± Î¹ : Type*} {f : Î¹ â†’ Set Î±}
    {c : Set Î¹} (hn : c.Nonempty) (hc : DirectedOn (fun i j => f i âŠ† f j) c) {s : Finset Î±}
    (hs : (s : Set Î±) âŠ† â‹ƒ i âˆˆ c, f i) : âˆƒ i âˆˆ c, (s : Set Î±) âŠ† f i := by
  rw [Set.biUnion_eq_iUnion] at hs
  haveI := hn.coe_sort
  simpa using (directed_comp.2 hc.directed_val).exists_mem_subset_of_finset_subset_biUnion hs
#align directed_on.exists_mem_subset_of_finset_subset_bUnion DirectedOn.exists_mem_subset_of_finset_subset_biUnion
