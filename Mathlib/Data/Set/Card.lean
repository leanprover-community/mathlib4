/-
Copyright (c) 2023 Peter Nelson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Peter Nelson
-/
import Mathlib.SetTheory.Cardinal.Finite
import Mathlib.Data.Set.Finite.Powerset

/-!
# Noncomputable Set Cardinality

We define the cardinality of set `s` as a term `Set.encard s : ‚Ñï‚àû` and a term `Set.ncard s : ‚Ñï`.
The latter takes the junk value of zero if `s` is infinite. Both functions are noncomputable, and
are defined in terms of `ENat.card` (which takes a type as its argument); this file can be seen
as an API for the same function in the special case where the type is a coercion of a `Set`,
allowing for smoother interactions with the `Set` API.

`Set.encard` never takes junk values, so is more mathematically natural than `Set.ncard`, even
though it takes values in a less convenient type. It is probably the right choice in settings where
one is concerned with the cardinalities of sets that may or may not be infinite.

`Set.ncard` has a nicer codomain, but when using it, `Set.Finite` hypotheses are normally needed to
make sure its values are meaningful.  More generally, `Set.ncard` is intended to be used over the
obvious alternative `Finset.card` when finiteness is 'propositional' rather than  'structural'.
When working with sets that are finite by virtue of their definition, then `Finset.card` probably
makes more sense. One setting where `Set.ncard` works nicely is in a type `Œ±` with `[Finite Œ±]`,
where every set is automatically finite. In this setting, we use default arguments and a simple
tactic so that finiteness goals are discharged automatically in `Set.ncard` theorems.

## Main Definitions

* `Set.encard s` is the cardinality of the set `s` as an extended natural number, with value `‚ä§` if
    `s` is infinite.
* `Set.ncard s` is the cardinality of the set `s` as a natural number, provided `s` is Finite.
  If `s` is Infinite, then `Set.ncard s = 0`.
* `toFinite_tac` is a tactic that tries to synthesize a `Set.Finite s` argument with
  `Set.toFinite`. This will work for `s : Set Œ±` where there is a `Finite Œ±` instance.

## Implementation Notes

The theorems in this file are very similar to those in `Data.Finset.Card`, but with `Set` operations
instead of `Finset`. We first prove all the theorems for `Set.encard`, and then derive most of the
`Set.ncard` results as a consequence. Things are done this way to avoid reliance on the `Finset` API
for theorems about infinite sets, and to allow for a refactor that removes or modifies `Set.ncard`
in the future.

Nearly all the theorems for `Set.ncard` require finiteness of one or more of their arguments. We
provide this assumption with a default argument of the form `(hs : s.Finite := by toFinite_tac)`,
where `toFinite_tac` will find an `s.Finite` term in the cases where `s` is a set in a `Finite`
type.

Often, where there are two set arguments `s` and `t`, the finiteness of one follows from the other
in the context of the theorem, in which case we only include the ones that are needed, and derive
the other inside the proof. A few of the theorems, such as `ncard_union_le` do not require
finiteness arguments; they are true by coincidence due to junk values.
-/

namespace Set

variable {Œ± Œ≤ : Type*} {s t : Set Œ±}

/-- The cardinality of a set as a term in `‚Ñï‚àû` -/
noncomputable def encard (s : Set Œ±) : ‚Ñï‚àû := ENat.card s

@[simp] theorem encard_univ (Œ± : Type*) :
    encard (univ : Set Œ±) = ENat.card Œ± := by
  rw [encard, ENat.card_congr (Equiv.Set.univ Œ±)]

@[simp] theorem _root_.ENat.card_coe_set_eq (s : Set Œ±) : ENat.card s = s.encard := rfl

@[deprecated "Use simp" (since := "2025-09-23")]
theorem encard_univ_coe (s : Set Œ±) : encard (univ : Set s) = encard s := by simp

theorem Finite.encard_eq_coe_toFinset_card (h : s.Finite) : s.encard = h.toFinset.card := by
  have := h.fintype
  rw [encard, ENat.card_eq_coe_fintype_card, toFinite_toFinset, toFinset_card]

theorem encard_eq_coe_toFinset_card (s : Set Œ±) [Fintype s] : encard s = s.toFinset.card := by
  have h := toFinite s
  rw [h.encard_eq_coe_toFinset_card, toFinite_toFinset]

@[simp] theorem toENat_cardinalMk (s : Set Œ±) : (Cardinal.mk s).toENat = s.encard := rfl

theorem toENat_cardinalMk_subtype (P : Œ± ‚Üí Prop) :
    (Cardinal.mk {x // P x}).toENat = {x | P x}.encard :=
  rfl

@[simp] theorem coe_fintypeCard (s : Set Œ±) [Fintype s] : Fintype.card s = s.encard := by
  simp [encard_eq_coe_toFinset_card]

@[simp, norm_cast] theorem encard_coe_eq_coe_finsetCard (s : Finset Œ±) :
    encard (s : Set Œ±) = s.card := by
  rw [Finite.encard_eq_coe_toFinset_card (Finset.finite_toSet s)]; simp

@[simp] theorem Infinite.encard_eq {s : Set Œ±} (h : s.Infinite) : s.encard = ‚ä§ := by
  have := h.to_subtype
  rw [encard, ENat.card_eq_top_of_infinite]

@[simp] theorem encard_eq_zero : s.encard = 0 ‚Üî s = ‚àÖ := by
  rw [encard, ENat.card_eq_zero_iff_empty, isEmpty_subtype, eq_empty_iff_forall_notMem]

@[simp] theorem encard_empty : (‚àÖ : Set Œ±).encard = 0 := by
  rw [encard_eq_zero]

theorem nonempty_of_encard_ne_zero (h : s.encard ‚â† 0) : s.Nonempty := by
  rwa [nonempty_iff_ne_empty, Ne, ‚Üê encard_eq_zero]

theorem encard_ne_zero : s.encard ‚â† 0 ‚Üî s.Nonempty := by
  rw [ne_eq, encard_eq_zero, nonempty_iff_ne_empty]

@[simp] theorem encard_pos : 0 < s.encard ‚Üî s.Nonempty := by
  rw [pos_iff_ne_zero, encard_ne_zero]

protected alias ‚ü®_, Nonempty.encard_pos‚ü© := encard_pos

@[simp] theorem encard_singleton (e : Œ±) : ({e} : Set Œ±).encard = 1 := by
  rw [encard, ENat.card_eq_coe_fintype_card, Fintype.card_ofSubsingleton, Nat.cast_one]

theorem encard_union_eq (h : Disjoint s t) : (s ‚à™ t).encard = s.encard + t.encard := by
  classical
  unfold encard
  simp [ENat.card_congr (Equiv.Set.union h)]

theorem encard_ne_add_one (a : Œ±) :
    ({x | x ‚â† a}).encard + 1 = ENat.card Œ± := by
  have : Disjoint {x | x ‚â† a} {a} := disjoint_singleton_right.mpr <| by simp
  replace this := (Set.encard_union_eq this).symm
  have aux : {x | x ‚â† a} ‚à™ {a} = univ := by ext x; simp [eq_or_ne x a]
  rwa [encard_singleton, aux, encard_univ] at this

theorem encard_insert_of_notMem {a : Œ±} (has : a ‚àâ s) : (insert a s).encard = s.encard + 1 := by
  rw [‚Üê union_singleton, encard_union_eq (by simpa), encard_singleton]

@[deprecated (since := "2025-05-23")] alias encard_insert_of_not_mem := encard_insert_of_notMem

theorem Finite.encard_lt_top (h : s.Finite) : s.encard < ‚ä§ := by
  induction s, h using Set.Finite.induction_on with
  | empty => simp
  | insert hat _ ht' =>
    rw [encard_insert_of_notMem hat]
    exact lt_tsub_iff_right.1 ht'

theorem Finite.encard_eq_coe (h : s.Finite) : s.encard = ENat.toNat s.encard :=
  (ENat.coe_toNat h.encard_lt_top.ne).symm

theorem Finite.exists_encard_eq_coe (h : s.Finite) : ‚àÉ (n : ‚Ñï), s.encard = n :=
  ‚ü®_, h.encard_eq_coe‚ü©

@[simp] theorem encard_lt_top_iff : s.encard < ‚ä§ ‚Üî s.Finite :=
  ‚ü®fun h ‚Ü¶ by_contra fun h' ‚Ü¶ h.ne (Infinite.encard_eq h'), Finite.encard_lt_top‚ü©

@[simp] theorem encard_eq_top_iff : s.encard = ‚ä§ ‚Üî s.Infinite := by
  rw [‚Üê not_iff_not, ‚Üê Ne, ‚Üê lt_top_iff_ne_top, encard_lt_top_iff, not_infinite]

alias ‚ü®_, encard_eq_top‚ü© := encard_eq_top_iff

theorem encard_ne_top_iff : s.encard ‚â† ‚ä§ ‚Üî s.Finite := by
  simp

theorem finite_of_encard_le_coe {k : ‚Ñï} (h : s.encard ‚â§ k) : s.Finite := by
  rw [‚Üê encard_lt_top_iff]; exact h.trans_lt (WithTop.coe_lt_top _)

theorem finite_of_encard_eq_coe {k : ‚Ñï} (h : s.encard = k) : s.Finite :=
  finite_of_encard_le_coe h.le

theorem encard_le_coe_iff {k : ‚Ñï} : s.encard ‚â§ k ‚Üî s.Finite ‚àß ‚àÉ (n‚ÇÄ : ‚Ñï), s.encard = n‚ÇÄ ‚àß n‚ÇÄ ‚â§ k :=
  ‚ü®fun h ‚Ü¶ ‚ü®finite_of_encard_le_coe h, by rwa [ENat.le_coe_iff] at h‚ü©,
    fun ‚ü®_,‚ü®n‚ÇÄ,hs, hle‚ü©‚ü© ‚Ü¶ by rwa [hs, Nat.cast_le]‚ü©

@[simp]
theorem encard_prod {s : Set Œ±} {t : Set Œ≤} : (s √óÀ¢ t).encard = s.encard * t.encard := by
  unfold encard
  simp [ENat.card_congr (Equiv.Set.prod ..)]

section Lattice

theorem encard_le_encard (h : s ‚äÜ t) : s.encard ‚â§ t.encard := by
  rw [‚Üê union_diff_cancel h, encard_union_eq disjoint_sdiff_right]; exact le_self_add

theorem encard_mono {Œ± : Type*} : Monotone (encard : Set Œ± ‚Üí ‚Ñï‚àû) :=
  fun _ _ ‚Ü¶ encard_le_encard

theorem encard_diff_add_encard_of_subset (h : s ‚äÜ t) : (t \ s).encard + s.encard = t.encard := by
  rw [‚Üê encard_union_eq disjoint_sdiff_left, diff_union_self, union_eq_self_of_subset_right h]

@[simp] theorem one_le_encard_iff_nonempty : 1 ‚â§ s.encard ‚Üî s.Nonempty := by
  rw [nonempty_iff_ne_empty, Ne, ‚Üê encard_eq_zero, ENat.one_le_iff_ne_zero]

theorem encard_diff_add_encard_inter (s t : Set Œ±) :
    (s \ t).encard + (s ‚à© t).encard = s.encard := by
  rw [‚Üê encard_union_eq (disjoint_of_subset_right inter_subset_right disjoint_sdiff_left),
    diff_union_inter]

theorem encard_union_add_encard_inter (s t : Set Œ±) :
    (s ‚à™ t).encard + (s ‚à© t).encard = s.encard + t.encard := by
  rw [‚Üê diff_union_self, encard_union_eq disjoint_sdiff_left, add_right_comm,
    encard_diff_add_encard_inter]

theorem encard_eq_encard_iff_encard_diff_eq_encard_diff (h : (s ‚à© t).Finite) :
    s.encard = t.encard ‚Üî (s \ t).encard = (t \ s).encard := by
  rw [‚Üê encard_diff_add_encard_inter s t, ‚Üê encard_diff_add_encard_inter t s, inter_comm t s,
    WithTop.add_right_inj h.encard_lt_top.ne]

theorem encard_le_encard_iff_encard_diff_le_encard_diff (h : (s ‚à© t).Finite) :
    s.encard ‚â§ t.encard ‚Üî (s \ t).encard ‚â§ (t \ s).encard := by
  rw [‚Üê encard_diff_add_encard_inter s t, ‚Üê encard_diff_add_encard_inter t s, inter_comm t s,
    WithTop.add_le_add_iff_right h.encard_lt_top.ne]

theorem encard_lt_encard_iff_encard_diff_lt_encard_diff (h : (s ‚à© t).Finite) :
    s.encard < t.encard ‚Üî (s \ t).encard < (t \ s).encard := by
  rw [‚Üê encard_diff_add_encard_inter s t, ‚Üê encard_diff_add_encard_inter t s, inter_comm t s,
    WithTop.add_lt_add_iff_right h.encard_lt_top.ne]

theorem encard_union_le (s t : Set Œ±) : (s ‚à™ t).encard ‚â§ s.encard + t.encard := by
  rw [‚Üê encard_union_add_encard_inter]; exact le_self_add

theorem finite_iff_finite_of_encard_eq_encard (h : s.encard = t.encard) : s.Finite ‚Üî t.Finite := by
  rw [‚Üê encard_lt_top_iff, ‚Üê encard_lt_top_iff, h]

theorem infinite_iff_infinite_of_encard_eq_encard (h : s.encard = t.encard) :
    s.Infinite ‚Üî t.Infinite := by rw [‚Üê encard_eq_top_iff, h, encard_eq_top_iff]

theorem Finite.finite_of_encard_le {s : Set Œ±} {t : Set Œ≤} (hs : s.Finite)
    (h : t.encard ‚â§ s.encard) : t.Finite :=
  encard_lt_top_iff.1 (h.trans_lt hs.encard_lt_top)

lemma Finite.eq_of_subset_of_encard_le' (ht : t.Finite) (hst : s ‚äÜ t) (hts : t.encard ‚â§ s.encard) :
    s = t := by
  rw [‚Üê zero_add (a := encard s), ‚Üê encard_diff_add_encard_of_subset hst] at hts
  have hdiff := WithTop.le_of_add_le_add_right (ht.subset hst).encard_lt_top.ne hts
  rw [nonpos_iff_eq_zero, encard_eq_zero, diff_eq_empty] at hdiff
  exact hst.antisymm hdiff

theorem Finite.eq_of_subset_of_encard_le (hs : s.Finite) (hst : s ‚äÜ t)
    (hts : t.encard ‚â§ s.encard) : s = t :=
  (hs.finite_of_encard_le hts).eq_of_subset_of_encard_le' hst hts

theorem Finite.encard_lt_encard (hs : s.Finite) (h : s ‚äÇ t) : s.encard < t.encard :=
  (encard_mono h.subset).lt_of_ne fun he ‚Ü¶ h.ne (hs.eq_of_subset_of_encard_le h.subset he.symm.le)

theorem encard_strictMono [Finite Œ±] : StrictMono (encard : Set Œ± ‚Üí ‚Ñï‚àû) :=
  fun _ _ h ‚Ü¶ (toFinite _).encard_lt_encard h

theorem encard_diff_add_encard (s t : Set Œ±) : (s \ t).encard + t.encard = (s ‚à™ t).encard := by
  rw [‚Üê encard_union_eq disjoint_sdiff_left, diff_union_self]

theorem encard_le_encard_diff_add_encard (s t : Set Œ±) : s.encard ‚â§ (s \ t).encard + t.encard :=
  (encard_mono subset_union_left).trans_eq (encard_diff_add_encard _ _).symm

theorem tsub_encard_le_encard_diff (s t : Set Œ±) : s.encard - t.encard ‚â§ (s \ t).encard := by
  rw [tsub_le_iff_left, add_comm]; apply encard_le_encard_diff_add_encard

theorem encard_add_encard_compl (s : Set Œ±) : s.encard + s·∂ú.encard = (univ : Set Œ±).encard := by
  rw [‚Üê encard_union_eq disjoint_compl_right, union_compl_self]

end Lattice

section InsertErase

variable {a b : Œ±}

theorem encard_insert_le (s : Set Œ±) (x : Œ±) : (insert x s).encard ‚â§ s.encard + 1 := by
  rw [‚Üê union_singleton, ‚Üê encard_singleton x]; apply encard_union_le

theorem encard_singleton_inter (s : Set Œ±) (x : Œ±) : ({x} ‚à© s).encard ‚â§ 1 := by
  rw [‚Üê encard_singleton x]; exact encard_le_encard inter_subset_left

theorem encard_diff_singleton_add_one (h : a ‚àà s) :
    (s \ {a}).encard + 1 = s.encard := by
  rw [‚Üê encard_insert_of_notMem (fun h ‚Ü¶ h.2 rfl), insert_diff_singleton, insert_eq_of_mem h]

theorem encard_diff_singleton_of_mem (h : a ‚àà s) :
    (s \ {a}).encard = s.encard - 1 := by
  rw [‚Üê encard_diff_singleton_add_one h, ‚Üê WithTop.add_right_inj WithTop.one_ne_top,
    tsub_add_cancel_of_le (self_le_add_left _ _)]

theorem encard_tsub_one_le_encard_diff_singleton (s : Set Œ±) (x : Œ±) :
    s.encard - 1 ‚â§ (s \ {x}).encard := by
  rw [‚Üê encard_singleton x]; apply tsub_encard_le_encard_diff

theorem encard_exchange (ha : a ‚àâ s) (hb : b ‚àà s) : (insert a (s \ {b})).encard = s.encard := by
  rw [encard_insert_of_notMem, encard_diff_singleton_add_one hb]
  simp_all only [mem_diff, mem_singleton_iff, false_and, not_false_eq_true]

theorem encard_exchange' (ha : a ‚àâ s) (hb : b ‚àà s) : (insert a s \ {b}).encard = s.encard := by
  rw [‚Üê insert_diff_singleton_comm (by rintro rfl; exact ha hb), encard_exchange ha hb]

theorem encard_eq_add_one_iff {k : ‚Ñï‚àû} :
    s.encard = k + 1 ‚Üî (‚àÉ a t, a ‚àâ t ‚àß insert a t = s ‚àß t.encard = k) := by
  refine ‚ü®fun h ‚Ü¶ ?_, ?_‚ü©
  ¬∑ obtain ‚ü®a, ha‚ü© := nonempty_of_encard_ne_zero (s := s) (by simp [h])
    refine ‚ü®a, s \ {a}, fun h ‚Ü¶ h.2 rfl, by rwa [insert_diff_singleton, insert_eq_of_mem], ?_‚ü©
    rw [‚Üê WithTop.add_right_inj WithTop.one_ne_top, ‚Üê h,
      encard_diff_singleton_add_one ha]
  rintro ‚ü®a, t, h, rfl, rfl‚ü©
  rw [encard_insert_of_notMem h]

/-- Every set is either empty, infinite, or can have its `encard` reduced by a removal. Intended
  for well-founded induction on the value of `encard`. -/
theorem eq_empty_or_encard_eq_top_or_encard_diff_singleton_lt (s : Set Œ±) :
    s = ‚àÖ ‚à® s.encard = ‚ä§ ‚à® ‚àÉ a ‚àà s, (s \ {a}).encard < s.encard := by
  refine s.eq_empty_or_nonempty.elim Or.inl (Or.inr ‚àò fun ‚ü®a,ha‚ü© ‚Ü¶
    (s.finite_or_infinite.elim (fun hfin ‚Ü¶ Or.inr ‚ü®a, ha, ?_‚ü©) (Or.inl ‚àò Infinite.encard_eq)))
  rw [‚Üê encard_diff_singleton_add_one ha]; nth_rw 1 [‚Üê add_zero (encard _)]
  exact WithTop.add_lt_add_left hfin.diff.encard_lt_top.ne zero_lt_one

end InsertErase

section SmallSets

theorem encard_pair {x y : Œ±} (hne : x ‚â† y) : ({x, y} : Set Œ±).encard = 2 := by
  rw [encard_insert_of_notMem (by simpa), ‚Üê one_add_one_eq_two,
    WithTop.add_right_inj WithTop.one_ne_top, encard_singleton]

theorem encard_eq_one : s.encard = 1 ‚Üî ‚àÉ x, s = {x} := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun ‚ü®x, hx‚ü© ‚Ü¶ by rw [hx, encard_singleton]‚ü©
  obtain ‚ü®x, hx‚ü© := nonempty_of_encard_ne_zero (s := s) (by rw [h]; simp)
  exact ‚ü®x, ((finite_singleton x).eq_of_subset_of_encard_le (by simpa) (by simp [h])).symm‚ü©

theorem encard_le_one_iff_eq : s.encard ‚â§ 1 ‚Üî s = ‚àÖ ‚à® ‚àÉ x, s = {x} := by
  rw [le_iff_lt_or_eq, lt_iff_not_ge, ENat.one_le_iff_ne_zero, not_not, encard_eq_zero,
    encard_eq_one]

theorem encard_le_one_iff : s.encard ‚â§ 1 ‚Üî ‚àÄ a b, a ‚àà s ‚Üí b ‚àà s ‚Üí a = b := by
  rw [encard_le_one_iff_eq, or_iff_not_imp_left, ‚Üê Ne, ‚Üê nonempty_iff_ne_empty]
  refine ‚ü®fun h a b has hbs ‚Ü¶ ?_,
    fun h ‚ü®x, hx‚ü© ‚Ü¶ ‚ü®x, ((singleton_subset_iff.2 hx).antisymm' (fun y hy ‚Ü¶ h _ _ hy hx))‚ü©‚ü©
  obtain ‚ü®x, rfl‚ü© := h ‚ü®_, has‚ü©
  rw [(has : a = x), (hbs : b = x)]

theorem encard_le_one_iff_subsingleton : s.encard ‚â§ 1 ‚Üî s.Subsingleton := by
  rw [encard_le_one_iff, Set.Subsingleton]
  tauto

theorem one_lt_encard_iff_nontrivial : 1 < s.encard ‚Üî s.Nontrivial := by
  rw [‚Üê not_iff_not, not_lt, Set.not_nontrivial_iff, ‚Üê encard_le_one_iff_subsingleton]

theorem one_lt_encard_iff : 1 < s.encard ‚Üî ‚àÉ a b, a ‚àà s ‚àß b ‚àà s ‚àß a ‚â† b := by
  rw [‚Üê not_iff_not, not_exists, not_lt, encard_le_one_iff]; aesop

theorem exists_ne_of_one_lt_encard (h : 1 < s.encard) (a : Œ±) : ‚àÉ b ‚àà s, b ‚â† a := by
  by_contra! h'
  obtain ‚ü®b, b', hb, hb', hne‚ü© := one_lt_encard_iff.1 h
  apply hne
  rw [h' b hb, h' b' hb']

theorem encard_eq_two : s.encard = 2 ‚Üî ‚àÉ x y, x ‚â† y ‚àß s = {x, y} := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun ‚ü®x, y, hne, hs‚ü© ‚Ü¶ by rw [hs, encard_pair hne]‚ü©
  obtain ‚ü®x, hx‚ü© := nonempty_of_encard_ne_zero (s := s) (by rw [h]; simp)
  rw [‚Üê insert_eq_of_mem hx, ‚Üê insert_diff_singleton, encard_insert_of_notMem (fun h ‚Ü¶ h.2 rfl),
    ‚Üê one_add_one_eq_two, WithTop.add_right_inj (WithTop.one_ne_top), encard_eq_one] at h
  obtain ‚ü®y, h‚ü© := h
  refine ‚ü®x, y, by rintro rfl; exact (h.symm.subset rfl).2 rfl, ?_‚ü©
  rw [‚Üê h, insert_diff_singleton, insert_eq_of_mem hx]

theorem encard_eq_three {Œ± : Type u_1} {s : Set Œ±} :
    encard s = 3 ‚Üî ‚àÉ x y z, x ‚â† y ‚àß x ‚â† z ‚àß y ‚â† z ‚àß s = {x, y, z} := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun ‚ü®x, y, z, hxy, hyz, hxz, hs‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®x, hx‚ü© := nonempty_of_encard_ne_zero (s := s) (by rw [h]; simp)
    rw [‚Üê insert_eq_of_mem hx, ‚Üê insert_diff_singleton,
      encard_insert_of_notMem (fun h ‚Ü¶ h.2 rfl), (by exact rfl : (3 : ‚Ñï‚àû) = 2 + 1),
      WithTop.add_right_inj WithTop.one_ne_top, encard_eq_two] at h
    obtain ‚ü®y, z, hne, hs‚ü© := h
    refine ‚ü®x, y, z, ?_, ?_, hne, ?_‚ü©
    ¬∑ rintro rfl; exact (hs.symm.subset (Or.inl rfl)).2 rfl
    ¬∑ rintro rfl; exact (hs.symm.subset (Or.inr rfl)).2 rfl
    rw [‚Üê hs, insert_diff_singleton, insert_eq_of_mem hx]
  rw [hs, encard_insert_of_notMem, encard_insert_of_notMem, encard_singleton] <;> aesop

theorem encard_eq_four {Œ± : Type u_1} {s : Set Œ±} :
    encard s = 4 ‚Üî ‚àÉ x y z w, x ‚â† y ‚àß x ‚â† z ‚àß x ‚â† w ‚àß y ‚â† z ‚àß y ‚â† w ‚àß z ‚â† w ‚àß s = {x, y, z, w} := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun ‚ü®x, y, z, w, hxy, hxz, hxw, hyz, hyw, hzw, hs‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®x, hx‚ü© := nonempty_of_encard_ne_zero (s := s) (by rw [h]; simp)
    rw [‚Üê insert_eq_of_mem hx, ‚Üê insert_diff_singleton,
      encard_insert_of_notMem (fun h ‚Ü¶ h.2 rfl), (by exact rfl : (4 : ‚Ñï‚àû) = 3 + 1),
      WithTop.add_right_inj WithTop.one_ne_top, encard_eq_three] at h
    obtain ‚ü®y, z, w, hyz, hyw, hzw, hs‚ü© := h
    refine ‚ü®x, y, z, w, ?_, ?_, ?_, hyz, hyw, hzw, ?_‚ü©
    ¬∑ rintro rfl; exact (hs.symm.subset (Or.inl rfl)).2 rfl
    ¬∑ rintro rfl; exact (hs.symm.subset (Or.inr (Or.inl rfl))).2 rfl
    ¬∑ rintro rfl; exact (hs.symm.subset (Or.inr (Or.inr rfl))).2 rfl
    rw [‚Üê hs, insert_diff_singleton, insert_eq_of_mem hx]
  rw [hs, encard_insert_of_notMem, encard_insert_of_notMem, encard_insert_of_notMem,
    encard_singleton] <;> aesop

theorem Nat.encard_range (k : ‚Ñï) : {i | i < k}.encard = k := by
  convert encard_coe_eq_coe_finsetCard (Finset.range k) using 1
  ¬∑ rw [Finset.coe_range, Iio_def]
  rw [Finset.card_range]

end SmallSets

theorem Finite.eq_insert_of_subset_of_encard_eq_succ (hs : s.Finite) (h : s ‚äÜ t)
    (hst : t.encard = s.encard + 1) : ‚àÉ a, t = insert a s := by
  rw [‚Üê encard_diff_add_encard_of_subset h, add_comm, WithTop.add_left_inj hs.encard_lt_top.ne,
    encard_eq_one] at hst
  obtain ‚ü®x, hx‚ü© := hst; use x; rw [‚Üê diff_union_of_subset h, hx, singleton_union]

theorem exists_subset_encard_eq {k : ‚Ñï‚àû} (hk : k ‚â§ s.encard) : ‚àÉ t, t ‚äÜ s ‚àß t.encard = k := by
  induction k using ENat.nat_induction with
  | zero => exact ‚ü®‚àÖ, empty_subset _, by simp‚ü©
  | succ n IH =>
    obtain ‚ü®t‚ÇÄ, ht‚ÇÄs, ht‚ÇÄ‚ü© := IH (le_trans (by simp) hk)
    simp only [Nat.cast_succ] at *
    have hne : t‚ÇÄ ‚â† s := by
      rintro rfl; rw [ht‚ÇÄ, ‚Üê Nat.cast_one, ‚Üê Nat.cast_add, Nat.cast_le] at hk; simp at hk
    obtain ‚ü®x, hx‚ü© := exists_of_ssubset (ht‚ÇÄs.ssubset_of_ne hne)
    exact ‚ü®insert x t‚ÇÄ, insert_subset hx.1 ht‚ÇÄs, by rw [encard_insert_of_notMem hx.2, ht‚ÇÄ]‚ü©
  | top => rw [top_le_iff] at hk; exact ‚ü®s, Subset.rfl, hk‚ü©

theorem exists_superset_subset_encard_eq {k : ‚Ñï‚àû}
    (hst : s ‚äÜ t) (hsk : s.encard ‚â§ k) (hkt : k ‚â§ t.encard) :
    ‚àÉ r, s ‚äÜ r ‚àß r ‚äÜ t ‚àß r.encard = k := by
  obtain (hs | hs) := eq_or_ne s.encard ‚ä§
  ¬∑ rw [hs, top_le_iff] at hsk; subst hsk; exact ‚ü®s, Subset.rfl, hst, hs‚ü©
  obtain ‚ü®k, rfl‚ü© := exists_add_of_le hsk
  obtain ‚ü®k', hk'‚ü© := exists_add_of_le hkt
  have hk : k ‚â§ encard (t \ s) := by
    rw [‚Üê encard_diff_add_encard_of_subset hst, add_comm] at hkt
    exact WithTop.le_of_add_le_add_right hs hkt
  obtain ‚ü®r', hr', rfl‚ü© := exists_subset_encard_eq hk
  refine ‚ü®s ‚à™ r', subset_union_left, union_subset hst (hr'.trans diff_subset), ?_‚ü©
  rw [encard_union_eq (disjoint_of_subset_right hr' disjoint_sdiff_right)]

section Function

variable {s : Set Œ±} {t : Set Œ≤} {f : Œ± ‚Üí Œ≤}

theorem InjOn.encard_image (h : InjOn f s) : (f '' s).encard = s.encard := by
  rw [encard, ENat.card_image_of_injOn h, encard]

theorem encard_congr (e : s ‚âÉ t) : s.encard = t.encard := ENat.card_congr e

theorem _root_.Function.Injective.encard_image (hf : f.Injective) (s : Set Œ±) :
    (f '' s).encard = s.encard :=
  hf.injOn.encard_image

theorem _root_.Function.Injective.encard_range (hf : f.Injective) :
    ENat.card Œ± ‚â§ (range f).encard := by
  rw [‚Üê image_univ, hf.encard_image, encard_univ]

theorem _root_.Function.Embedding.encard_le (e : s ‚Ü™ t) : s.encard ‚â§ t.encard :=
  ENat.card_le_card_of_injective e.injective

theorem encard_image_le (f : Œ± ‚Üí Œ≤) (s : Set Œ±) : (f '' s).encard ‚â§ s.encard := by
  obtain (h | h) := isEmpty_or_nonempty Œ±
  ¬∑ rw [s.eq_empty_of_isEmpty]; simp
  rw [‚Üê (f.invFunOn_injOn_image s).encard_image]
  apply encard_le_encard
  exact f.invFunOn_image_image_subset s

theorem Finite.injOn_of_encard_image_eq (hs : s.Finite) (h : (f '' s).encard = s.encard) :
    InjOn f s := by
  obtain (h' | hne) := isEmpty_or_nonempty Œ±
  ¬∑ simp
  rw [‚Üê (f.invFunOn_injOn_image s).encard_image] at h
  rw [injOn_iff_invFunOn_image_image_eq_self]
  exact hs.eq_of_subset_of_encard_le' (f.invFunOn_image_image_subset s) h.symm.le

theorem encard_preimage_of_injective_subset_range (hf : f.Injective) (ht : t ‚äÜ range f) :
    (f ‚Åª¬π' t).encard = t.encard := by
  rw [‚Üê hf.encard_image, image_preimage_eq_inter_range, inter_eq_self_of_subset_left ht]

lemma encard_preimage_of_bijective (hf : f.Bijective) (t : Set Œ≤) : (f ‚Åª¬π' t).encard = t.encard :=
  encard_preimage_of_injective_subset_range hf.injective (by simp [hf.surjective.range_eq])

theorem encard_le_encard_of_injOn (hf : MapsTo f s t) (f_inj : InjOn f s) :
    s.encard ‚â§ t.encard := by
  rw [‚Üê f_inj.encard_image]; apply encard_le_encard; rintro _ ‚ü®x, hx, rfl‚ü©; exact hf hx

open Notation in
lemma encard_preimage_val_le_encard_left (P Q : Set Œ±) : (P ‚Üì‚à© Q).encard ‚â§ P.encard :=
  (Function.Embedding.subtype _).encard_le

open Notation in
lemma encard_preimage_val_le_encard_right (P Q : Set Œ±) : (P ‚Üì‚à© Q).encard ‚â§ Q.encard :=
  Function.Embedding.encard_le ‚ü®fun ‚ü®‚ü®x, _‚ü©, hx‚ü© ‚Ü¶ ‚ü®x, hx‚ü©, fun _ _ h ‚Ü¶ by
    simpa [Subtype.coe_inj] using h‚ü©

theorem Finite.exists_injOn_of_encard_le [Nonempty Œ≤] {s : Set Œ±} {t : Set Œ≤} (hs : s.Finite)
    (hle : s.encard ‚â§ t.encard) : ‚àÉ (f : Œ± ‚Üí Œ≤), s ‚äÜ f ‚Åª¬π' t ‚àß InjOn f s := by
  classical
  obtain (rfl | h | ‚ü®a, has, -‚ü©) := s.eq_empty_or_encard_eq_top_or_encard_diff_singleton_lt
  ¬∑ simp
  ¬∑ exact (encard_ne_top_iff.mpr hs h).elim
  obtain ‚ü®b, hbt‚ü© := encard_pos.1 ((encard_pos.2 ‚ü®_, has‚ü©).trans_le hle)
  have hle' : (s \ {a}).encard ‚â§ (t \ {b}).encard := by
    rwa [‚Üê WithTop.add_le_add_iff_right WithTop.one_ne_top,
    encard_diff_singleton_add_one has, encard_diff_singleton_add_one hbt]
  obtain ‚ü®f‚ÇÄ, hf‚ÇÄs, hinj‚ü© := exists_injOn_of_encard_le hs.diff hle'
  simp only [preimage_diff, subset_def, mem_diff, mem_singleton_iff, mem_preimage, and_imp] at hf‚ÇÄs
  use Function.update f‚ÇÄ a b
  rw [‚Üê insert_eq_of_mem has, ‚Üê insert_diff_singleton, injOn_insert (fun h ‚Ü¶ h.2 rfl)]
  simp only [mem_diff, mem_singleton_iff, insert_diff_singleton, subset_def,
    mem_insert_iff, mem_preimage, Function.update_apply, forall_eq_or_imp, ite_true, and_imp,
    mem_image, ite_eq_left_iff, not_exists, not_and, not_forall, exists_prop, and_iff_right hbt]
  refine ‚ü®?_, ?_, fun x hxs hxa ‚Ü¶ ‚ü®hxa, (hf‚ÇÄs x hxs hxa).2‚ü©‚ü©
  ¬∑ rintro x hx; split_ifs with h
    ¬∑ assumption
    ¬∑ exact (hf‚ÇÄs x hx h).1
  exact InjOn.congr hinj (fun x ‚ü®_, hxa‚ü© ‚Ü¶ by rwa [Function.update_of_ne])
termination_by encard s

theorem Finite.exists_bijOn_of_encard_eq [Nonempty Œ≤] (hs : s.Finite) (h : s.encard = t.encard) :
    ‚àÉ (f : Œ± ‚Üí Œ≤), BijOn f s t := by
  obtain ‚ü®f, hf, hinj‚ü© := hs.exists_injOn_of_encard_le h.le; use f
  convert hinj.bijOn_image
  rw [(hs.image f).eq_of_subset_of_encard_le (image_subset_iff.mpr hf)
    (h.symm.trans hinj.encard_image.symm).le]

/-- A version of the pigeonhole principle for `Set`s rather than `Finset`s.

See also `Finset.exists_ne_map_eq_of_card_lt_of_maps_to` and
`Set.exists_ne_map_eq_of_ncard_lt_of_maps_to`. -/
lemma exists_ne_map_eq_of_encard_lt_of_maps_to (hc : t.encard < s.encard) (hf : MapsTo f s t) :
    ‚àÉ·µâ (a‚ÇÅ ‚àà s) (a‚ÇÇ ‚àà s), a‚ÇÅ ‚â† a‚ÇÇ ‚àß f a‚ÇÅ = f a‚ÇÇ := by
  contrapose! hc
  suffices Function.Injective (hf.restrict f) by
    let f' : s ‚Ü™ t := ‚ü®hf.restrict, this‚ü©
    exact f'.encard_le
  simpa only [hf.restrict_inj, not_imp_not] using hc

end Function

section ncard

open Nat

/-- A tactic (for use in default params) that applies `Set.toFinite` to synthesize a `Set.Finite`
  term. -/
syntax "toFinite_tac" : tactic

macro_rules
  | `(tactic| toFinite_tac) => `(tactic| apply Set.toFinite)

/-- A tactic useful for transferring proofs for `encard` to their corresponding `card` statements -/
syntax "to_encard_tac" : tactic

macro_rules
  | `(tactic| to_encard_tac) => `(tactic|
      simp only [‚Üê Nat.cast_le (Œ± := ‚Ñï‚àû), ‚Üê Nat.cast_inj (R := ‚Ñï‚àû), Nat.cast_add, Nat.cast_one])


/-- The cardinality of `s : Set Œ±` . Has the junk value `0` if `s` is infinite -/
noncomputable def ncard (s : Set Œ±) : ‚Ñï := ENat.toNat s.encard

theorem ncard_def (s : Set Œ±) : s.ncard = ENat.toNat s.encard := rfl

theorem Finite.cast_ncard_eq (hs : s.Finite) : s.ncard = s.encard := by
  rwa [ncard, ENat.coe_toNat_eq_self, ne_eq, encard_eq_top_iff, Set.Infinite, not_not]

lemma ncard_le_encard (s : Set Œ±) : s.ncard ‚â§ s.encard := ENat.coe_toNat_le_self _

@[simp] theorem _root_.Nat.card_coe_set_eq (s : Set Œ±) : Nat.card s = s.ncard := rfl

@[deprecated (since := "2025-07-05")] alias Nat.card_coe_set_eq := _root_.Nat.card_coe_set_eq

theorem ncard_eq_toFinset_card (s : Set Œ±) (hs : s.Finite := by toFinite_tac) :
    s.ncard = hs.toFinset.card := by
  rw [‚Üê _root_.Nat.card_coe_set_eq, @Nat.card_eq_fintype_card _ hs.fintype,
    @Finite.card_toFinset _ _ hs.fintype hs]

theorem ncard_eq_toFinset_card' (s : Set Œ±) [Fintype s] :
    s.ncard = s.toFinset.card := by
  simp [‚Üê _root_.Nat.card_coe_set_eq, Nat.card_eq_fintype_card]

lemma cast_ncard {s : Set Œ±} (hs : s.Finite) :
    (s.ncard : Cardinal) = Cardinal.mk s := @Nat.cast_card _ hs

theorem encard_le_coe_iff_finite_ncard_le {k : ‚Ñï} : s.encard ‚â§ k ‚Üî s.Finite ‚àß s.ncard ‚â§ k := by
  rw [encard_le_coe_iff, and_congr_right_iff]
  exact fun hfin ‚Ü¶ ‚ü®fun ‚ü®n‚ÇÄ, hn‚ÇÄ, hle‚ü© ‚Ü¶ by rwa [ncard_def, hn‚ÇÄ, ENat.toNat_coe],
    fun h ‚Ü¶ ‚ü®s.ncard, by rw [hfin.cast_ncard_eq], h‚ü©‚ü©

theorem Infinite.ncard (hs : s.Infinite) : s.ncard = 0 := by
  rw [‚Üê _root_.Nat.card_coe_set_eq, @Nat.card_eq_zero_of_infinite _ hs.to_subtype]

@[gcongr]
theorem ncard_le_ncard (hst : s ‚äÜ t) (ht : t.Finite := by toFinite_tac) :
    s.ncard ‚â§ t.ncard := by
  rw [‚Üê Nat.cast_le (Œ± := ‚Ñï‚àû), ht.cast_ncard_eq, (ht.subset hst).cast_ncard_eq]
  exact encard_mono hst

theorem ncard_mono [Finite Œ±] : @Monotone (Set Œ±) _ _ _ ncard := fun _ _ ‚Ü¶ ncard_le_ncard

@[simp] theorem ncard_eq_zero (hs : s.Finite := by toFinite_tac) :
    s.ncard = 0 ‚Üî s = ‚àÖ := by
  rw [‚Üê Nat.cast_inj (R := ‚Ñï‚àû), hs.cast_ncard_eq, Nat.cast_zero, encard_eq_zero]

@[simp, norm_cast] theorem ncard_coe_finset (s : Finset Œ±) : (s : Set Œ±).ncard = s.card := by
  rw [ncard_eq_toFinset_card _, Finset.finite_toSet_toFinset]

@[deprecated (since := "2025-07-05")] alias ncard_coe_Finset := ncard_coe_finset

@[simp] theorem ncard_univ (Œ± : Type*) : (univ : Set Œ±).ncard = Nat.card Œ± := Nat.card_univ

theorem ncard_le_card [Finite Œ±] (s : Set Œ±) : s.ncard ‚â§ Nat.card Œ± :=
  ncard_univ Œ± ‚ñ∏ ncard_le_ncard s.subset_univ

@[simp] theorem ncard_empty (Œ± : Type*) : (‚àÖ : Set Œ±).ncard = 0 := by
  rw [ncard_eq_zero]

theorem ncard_pos (hs : s.Finite := by toFinite_tac) : 0 < s.ncard ‚Üî s.Nonempty := by
  rw [pos_iff_ne_zero, Ne, ncard_eq_zero hs, nonempty_iff_ne_empty]

protected alias ‚ü®_, Nonempty.ncard_pos‚ü© := ncard_pos

theorem ncard_ne_zero_of_mem {a : Œ±} (h : a ‚àà s) (hs : s.Finite := by toFinite_tac) : s.ncard ‚â† 0 :=
  ((ncard_pos hs).mpr ‚ü®a, h‚ü©).ne.symm

theorem finite_of_ncard_ne_zero (hs : s.ncard ‚â† 0) : s.Finite :=
  s.finite_or_infinite.elim id fun h ‚Ü¶ (hs h.ncard).elim

theorem finite_of_ncard_pos (hs : 0 < s.ncard) : s.Finite :=
  finite_of_ncard_ne_zero hs.ne.symm

theorem nonempty_of_ncard_ne_zero (hs : s.ncard ‚â† 0) : s.Nonempty := by
  rw [nonempty_iff_ne_empty]; rintro rfl; simp at hs

@[simp] theorem ncard_singleton (a : Œ±) : ({a} : Set Œ±).ncard = 1 := by
  simp [ncard]

theorem ncard_singleton_inter (a : Œ±) (s : Set Œ±) : ({a} ‚à© s).ncard ‚â§ 1 := by
  rw [‚Üê Nat.cast_le (Œ± := ‚Ñï‚àû), (toFinite _).cast_ncard_eq, Nat.cast_one]
  apply encard_singleton_inter

@[simp]
theorem ncard_prod {s : Set Œ±} {t : Set Œ≤} : (s √óÀ¢ t).ncard = s.ncard * t.ncard := by
  simp [ncard, ENat.toNat_mul]

@[simp]
theorem ncard_powerset (s : Set Œ±) (hs : s.Finite := by toFinite_tac) :
    (ùí´ s).ncard = 2 ^ s.ncard := by
  have h := Cardinal.mk_powerset s
  rw [‚Üê cast_ncard hs.powerset, ‚Üê cast_ncard hs] at h
  norm_cast at h

section InsertErase

@[simp] theorem ncard_insert_of_notMem {a : Œ±} (h : a ‚àâ s) (hs : s.Finite := by toFinite_tac) :
    (insert a s).ncard = s.ncard + 1 := by
  rw [‚Üê Nat.cast_inj (R := ‚Ñï‚àû), (hs.insert a).cast_ncard_eq, Nat.cast_add, Nat.cast_one,
    hs.cast_ncard_eq, encard_insert_of_notMem h]

@[deprecated (since := "2025-05-23")] alias ncard_insert_of_not_mem := ncard_insert_of_notMem

theorem ncard_insert_of_mem {a : Œ±} (h : a ‚àà s) : ncard (insert a s) = s.ncard := by
  rw [insert_eq_of_mem h]

theorem ncard_insert_le (a : Œ±) (s : Set Œ±) : (insert a s).ncard ‚â§ s.ncard + 1 := by
  obtain hs | hs := s.finite_or_infinite
  ¬∑ to_encard_tac; rw [hs.cast_ncard_eq, (hs.insert _).cast_ncard_eq]; apply encard_insert_le
  rw [(hs.mono (subset_insert a s)).ncard]
  exact Nat.zero_le _

theorem ncard_insert_eq_ite {a : Œ±} [Decidable (a ‚àà s)] (hs : s.Finite := by toFinite_tac) :
    ncard (insert a s) = if a ‚àà s then s.ncard else s.ncard + 1 := by
  by_cases h : a ‚àà s
  ¬∑ rw [ncard_insert_of_mem h, if_pos h]
  ¬∑ rw [ncard_insert_of_notMem h hs, if_neg h]

theorem ncard_le_ncard_insert (a : Œ±) (s : Set Œ±) : s.ncard ‚â§ (insert a s).ncard := by
  classical
  refine
    s.finite_or_infinite.elim (fun h ‚Ü¶ ?_) (fun h ‚Ü¶ by (rw [h.ncard]; exact Nat.zero_le _))
  rw [ncard_insert_eq_ite h]; split_ifs <;> simp

theorem ncard_pair {a b : Œ±} (h : a ‚â† b) : ({a, b} : Set Œ±).ncard = 2 := by
  simp [h]

-- removing `@[simp]` because the LHS is not in simp normal form
theorem ncard_diff_singleton_add_one {a : Œ±} (h : a ‚àà s)
    (hs : s.Finite := by toFinite_tac) : (s \ {a}).ncard + 1 = s.ncard := by
  to_encard_tac
  rw [hs.cast_ncard_eq, hs.diff.cast_ncard_eq, encard_diff_singleton_add_one h]

@[simp] theorem ncard_diff_singleton_of_mem {a : Œ±} (h : a ‚àà s) :
    (s \ {a}).ncard = s.ncard - 1 := by
  rcases s.infinite_or_finite with hs | hs
  ¬∑ simp_all [ncard, Infinite.diff hs (finite_singleton a)]
  ¬∑ exact eq_tsub_of_add_eq (ncard_diff_singleton_add_one h hs)

theorem ncard_diff_singleton_lt_of_mem {a : Œ±} (h : a ‚àà s) (hs : s.Finite := by toFinite_tac) :
    (s \ {a}).ncard < s.ncard := by
  rw [‚Üê ncard_diff_singleton_add_one h hs]; apply lt_add_one

theorem ncard_diff_singleton_le (s : Set Œ±) (a : Œ±) : (s \ {a}).ncard ‚â§ s.ncard := by
  obtain hs | hs := s.finite_or_infinite
  ¬∑ apply ncard_le_ncard diff_subset hs
  convert zero_le (Œ± := ‚Ñï) _
  exact (hs.diff (by simp : Set.Finite {a})).ncard

theorem pred_ncard_le_ncard_diff_singleton (s : Set Œ±) (a : Œ±) : s.ncard - 1 ‚â§ (s \ {a}).ncard := by
  by_cases h : a ‚àà s
  ¬∑ rw [ncard_diff_singleton_of_mem h]
  rw [diff_singleton_eq_self h]
  apply Nat.pred_le

theorem ncard_exchange {a b : Œ±} (ha : a ‚àâ s) (hb : b ‚àà s) : (insert a (s \ {b})).ncard = s.ncard :=
  congr_arg ENat.toNat <| encard_exchange ha hb

theorem ncard_exchange' {a b : Œ±} (ha : a ‚àâ s) (hb : b ‚àà s) :
    (insert a s \ {b}).ncard = s.ncard := by
  rw [‚Üê ncard_exchange ha hb, ‚Üê singleton_union, ‚Üê singleton_union, union_diff_distrib,
    diff_singleton_eq_self fun h ‚Ü¶ ha (by rwa [‚Üê mem_singleton_iff.mp h])]

lemma odd_card_insert_iff {a : Œ±} (ha : a ‚àâ s) (hs : s.Finite := by toFinite_tac) :
    Odd (insert a s).ncard ‚Üî Even s.ncard := by
  rw [ncard_insert_of_notMem ha hs, Nat.odd_add]
  simp only [‚Üê Nat.not_even_iff_odd, Nat.not_even_one, iff_false, Decidable.not_not]

lemma even_card_insert_iff {a : Œ±} (ha : a ‚àâ s) (hs : s.Finite := by toFinite_tac) :
    Even (insert a s).ncard ‚Üî Odd s.ncard := by
  rw [ncard_insert_of_notMem ha hs, Nat.even_add_one, Nat.not_even_iff_odd]

end InsertErase

variable {f : Œ± ‚Üí Œ≤}

theorem ncard_image_le (hs : s.Finite := by toFinite_tac) : (f '' s).ncard ‚â§ s.ncard := by
  to_encard_tac; rw [hs.cast_ncard_eq, (hs.image _).cast_ncard_eq]; apply encard_image_le

theorem ncard_image_of_injOn (H : Set.InjOn f s) : (f '' s).ncard = s.ncard :=
  congr_arg ENat.toNat <| H.encard_image

theorem injOn_of_ncard_image_eq (h : (f '' s).ncard = s.ncard) (hs : s.Finite := by toFinite_tac) :
    Set.InjOn f s := by
  rw [‚Üê Nat.cast_inj (R := ‚Ñï‚àû), hs.cast_ncard_eq, (hs.image _).cast_ncard_eq] at h
  exact hs.injOn_of_encard_image_eq h

theorem ncard_image_iff (hs : s.Finite := by toFinite_tac) :
    (f '' s).ncard = s.ncard ‚Üî Set.InjOn f s :=
  ‚ü®fun h ‚Ü¶ injOn_of_ncard_image_eq h hs, ncard_image_of_injOn‚ü©

theorem ncard_image_of_injective (s : Set Œ±) (H : f.Injective) : (f '' s).ncard = s.ncard :=
  ncard_image_of_injOn fun _ _ _ _ h ‚Ü¶ H h

theorem ncard_preimage_of_injective_subset_range {s : Set Œ≤} (H : f.Injective)
    (hs : s ‚äÜ Set.range f) :
    (f ‚Åª¬π' s).ncard = s.ncard := by
  rw [‚Üê ncard_image_of_injective _ H, image_preimage_eq_iff.mpr hs]

theorem fiber_ncard_ne_zero_iff_mem_image {y : Œ≤} (hs : s.Finite := by toFinite_tac) :
    { x ‚àà s | f x = y }.ncard ‚â† 0 ‚Üî y ‚àà f '' s := by
  refine ‚ü®nonempty_of_ncard_ne_zero, ?_‚ü©
  rintro ‚ü®z, hz, rfl‚ü©
  exact @ncard_ne_zero_of_mem _ ({ x ‚àà s | f x = f z }) z (mem_sep hz rfl)
    (hs.subset (sep_subset _ _))

@[simp] theorem ncard_map (f : Œ± ‚Ü™ Œ≤) : (f '' s).ncard = s.ncard :=
  ncard_image_of_injective _ f.inj'

@[simp] theorem ncard_subtype (P : Œ± ‚Üí Prop) (s : Set Œ±) :
    { x : Subtype P | (x : Œ±) ‚àà s }.ncard = (s ‚à© setOf P).ncard := by
  convert (ncard_image_of_injective _ (@Subtype.coe_injective _ P)).symm
  ext x
  simp [‚Üê and_assoc, exists_eq_right]

theorem ncard_inter_le_ncard_left (s t : Set Œ±) (hs : s.Finite := by toFinite_tac) :
    (s ‚à© t).ncard ‚â§ s.ncard :=
  ncard_le_ncard inter_subset_left hs

theorem ncard_inter_le_ncard_right (s t : Set Œ±) (ht : t.Finite := by toFinite_tac) :
    (s ‚à© t).ncard ‚â§ t.ncard :=
  ncard_le_ncard inter_subset_right ht

theorem eq_of_subset_of_ncard_le (h : s ‚äÜ t) (h' : t.ncard ‚â§ s.ncard)
    (ht : t.Finite := by toFinite_tac) : s = t :=
  ht.eq_of_subset_of_encard_le' h
    (by rwa [‚Üê Nat.cast_le (Œ± := ‚Ñï‚àû), ht.cast_ncard_eq, (ht.subset h).cast_ncard_eq] at h')

theorem subset_iff_eq_of_ncard_le (h : t.ncard ‚â§ s.ncard) (ht : t.Finite := by toFinite_tac) :
    s ‚äÜ t ‚Üî s = t :=
  ‚ü®fun hst ‚Ü¶ eq_of_subset_of_ncard_le hst h ht, Eq.subset'‚ü©

theorem map_eq_of_subset {f : Œ± ‚Ü™ Œ±} (h : f '' s ‚äÜ s) (hs : s.Finite := by toFinite_tac) :
    f '' s = s :=
  eq_of_subset_of_ncard_le h (ncard_map _).ge hs

theorem sep_of_ncard_eq {a : Œ±} {P : Œ± ‚Üí Prop} (h : { x ‚àà s | P x }.ncard = s.ncard) (ha : a ‚àà s)
    (hs : s.Finite := by toFinite_tac) : P a :=
  sep_eq_self_iff_mem_true.mp (eq_of_subset_of_ncard_le (by simp) h.symm.le hs) _ ha

theorem ncard_lt_ncard (h : s ‚äÇ t) (ht : t.Finite := by toFinite_tac) :
    s.ncard < t.ncard := by
  rw [‚Üê Nat.cast_lt (Œ± := ‚Ñï‚àû), ht.cast_ncard_eq, (ht.subset h.subset).cast_ncard_eq]
  exact (ht.subset h.subset).encard_lt_encard h

theorem ncard_lt_card [Finite Œ±] (h : s ‚â† univ) : s.ncard < Nat.card Œ± :=
  ncard_univ Œ± ‚ñ∏ ncard_lt_ncard (ssubset_univ_iff.mpr h)

theorem ncard_strictMono [Finite Œ±] : @StrictMono (Set Œ±) _ _ _ ncard :=
  fun _ _ h ‚Ü¶ ncard_lt_ncard h

theorem ncard_eq_of_bijective {n : ‚Ñï} (f : ‚àÄ i, i < n ‚Üí Œ±)
    (hf : ‚àÄ a ‚àà s, ‚àÉ i, ‚àÉ h : i < n, f i h = a) (hf' : ‚àÄ (i) (h : i < n), f i h ‚àà s)
    (f_inj : ‚àÄ (i j) (hi : i < n) (hj : j < n), f i hi = f j hj ‚Üí i = j) : s.ncard = n := by
  let f' : Fin n ‚Üí Œ± := fun i ‚Ü¶ f i.val i.is_lt
  suffices himage : s = f' '' Set.univ by
    rw [‚Üê Fintype.card_fin n, ‚Üê Nat.card_eq_fintype_card, ‚Üê Set.ncard_univ, himage]
    exact ncard_image_of_injOn <| fun i _hi j _hj h ‚Ü¶ Fin.ext <| f_inj i.val j.val i.is_lt j.is_lt h
  ext x
  simp only [image_univ, mem_range]
  refine ‚ü®fun hx ‚Ü¶ ?_, fun ‚ü®‚ü®i, hi‚ü©, hx‚ü© ‚Ü¶ hx ‚ñ∏ hf' i hi‚ü©
  obtain ‚ü®i, hi, rfl‚ü© := hf x hx
  use ‚ü®i, hi‚ü©

theorem ncard_congr {t : Set Œ≤} (f : ‚àÄ a ‚àà s, Œ≤) (h‚ÇÅ : ‚àÄ a ha, f a ha ‚àà t)
    (h‚ÇÇ : ‚àÄ a b ha hb, f a ha = f b hb ‚Üí a = b) (h‚ÇÉ : ‚àÄ b ‚àà t, ‚àÉ a ha, f a ha = b) :
    s.ncard = t.ncard := by
  set f' : s ‚Üí t := fun x ‚Ü¶ ‚ü®f x.1 x.2, h‚ÇÅ _ _‚ü©
  have hbij : f'.Bijective := by
    constructor
    ¬∑ rintro ‚ü®x, hx‚ü© ‚ü®y, hy‚ü© hxy
      simp only [f', Subtype.mk.injEq] at hxy ‚ä¢
      exact h‚ÇÇ _ _ hx hy hxy
    rintro ‚ü®y, hy‚ü©
    obtain ‚ü®a, ha, rfl‚ü© := h‚ÇÉ y hy
    simp only [Subtype.exists]
    exact ‚ü®_, ha, rfl‚ü©
  simp_rw [‚Üê _root_.Nat.card_coe_set_eq]
  exact Nat.card_congr (Equiv.ofBijective f' hbij)

theorem ncard_le_ncard_of_injOn {t : Set Œ≤} (f : Œ± ‚Üí Œ≤) (hf : ‚àÄ a ‚àà s, f a ‚àà t) (f_inj : InjOn f s)
    (ht : t.Finite := by toFinite_tac) :
    s.ncard ‚â§ t.ncard := by
  have hle := encard_le_encard_of_injOn hf f_inj
  to_encard_tac; rwa [ht.cast_ncard_eq, (ht.finite_of_encard_le hle).cast_ncard_eq]

theorem ncard_range_of_injective (hf : Function.Injective f) :
    (range f).ncard = Nat.card Œ± := by
  rw [‚Üê image_univ, ncard_image_of_injective univ hf, ncard_univ]

/-- A version of the pigeonhole principle for `Set`s rather than `Finset`s.

See also `Finset.exists_ne_map_eq_of_card_lt_of_maps_to` and
`Set.exists_ne_map_eq_of_encard_lt_of_maps_to`. -/
theorem exists_ne_map_eq_of_ncard_lt_of_maps_to {t : Set Œ≤} (hc : t.ncard < s.ncard) {f : Œ± ‚Üí Œ≤}
    (hf : ‚àÄ a ‚àà s, f a ‚àà t) (ht : t.Finite := by toFinite_tac) :
    ‚àÉ x ‚àà s, ‚àÉ y ‚àà s, x ‚â† y ‚àß f x = f y := by
  by_contra h'
  simp only [Ne, not_exists, not_and, not_imp_not] at h'
  exact (ncard_le_ncard_of_injOn f hf h' ht).not_gt hc

theorem le_ncard_of_inj_on_range {n : ‚Ñï} (f : ‚Ñï ‚Üí Œ±) (hf : ‚àÄ i < n, f i ‚àà s)
    (f_inj : ‚àÄ i < n, ‚àÄ j < n, f i = f j ‚Üí i = j) (hs : s.Finite := by toFinite_tac) :
    n ‚â§ s.ncard := by
  rw [ncard_eq_toFinset_card _ hs]
  apply Finset.le_card_of_inj_on_range <;> simpa

theorem surj_on_of_inj_on_of_ncard_le {t : Set Œ≤} (f : ‚àÄ a ‚àà s, Œ≤) (hf : ‚àÄ a ha, f a ha ‚àà t)
    (hinj : ‚àÄ a‚ÇÅ a‚ÇÇ ha‚ÇÅ ha‚ÇÇ, f a‚ÇÅ ha‚ÇÅ = f a‚ÇÇ ha‚ÇÇ ‚Üí a‚ÇÅ = a‚ÇÇ) (hst : t.ncard ‚â§ s.ncard)
    (ht : t.Finite := by toFinite_tac) :
    ‚àÄ b ‚àà t, ‚àÉ a ha, b = f a ha := by
  intro b hb
  set f' : s ‚Üí t := fun x ‚Ü¶ ‚ü®f x.1 x.2, hf _ _‚ü©
  have finj : f'.Injective := by
    rintro ‚ü®x, hx‚ü© ‚ü®y, hy‚ü© hxy
    simp only [f', Subtype.mk.injEq] at hxy ‚ä¢
    apply hinj _ _ hx hy hxy
  have hft := ht.fintype
  have hft' := Fintype.ofInjective f' finj
  set f'' : ‚àÄ a, a ‚àà s.toFinset ‚Üí Œ≤ := fun a h ‚Ü¶ f a (by simpa using h)
  convert @Finset.surj_on_of_inj_on_of_card_le _ _ _ t.toFinset f'' _ _ _ _ (by simpa) using 1
  ¬∑ simp [f'']
  ¬∑ simp [f'', hf]
  ¬∑ intro a‚ÇÅ a‚ÇÇ ha‚ÇÅ ha‚ÇÇ h
    rw [mem_toFinset] at ha‚ÇÅ ha‚ÇÇ
    exact hinj _ _ ha‚ÇÅ ha‚ÇÇ h
  rwa [‚Üê ncard_eq_toFinset_card', ‚Üê ncard_eq_toFinset_card']

theorem inj_on_of_surj_on_of_ncard_le {t : Set Œ≤} (f : ‚àÄ a ‚àà s, Œ≤) (hf : ‚àÄ a ha, f a ha ‚àà t)
    (hsurj : ‚àÄ b ‚àà t, ‚àÉ a ha, f a ha = b) (hst : s.ncard ‚â§ t.ncard) ‚¶Éa‚ÇÅ‚¶Ñ (ha‚ÇÅ : a‚ÇÅ ‚àà s) ‚¶Éa‚ÇÇ‚¶Ñ
    (ha‚ÇÇ : a‚ÇÇ ‚àà s) (ha‚ÇÅa‚ÇÇ : f a‚ÇÅ ha‚ÇÅ = f a‚ÇÇ ha‚ÇÇ) (hs : s.Finite := by toFinite_tac) :
    a‚ÇÅ = a‚ÇÇ := by
  classical
  set f' : s ‚Üí t := fun x ‚Ü¶ ‚ü®f x.1 x.2, hf _ _‚ü©
  have hsurj : f'.Surjective := by
    rintro ‚ü®y, hy‚ü©
    obtain ‚ü®a, ha, rfl‚ü© := hsurj y hy
    simp only [Subtype.exists]
    exact ‚ü®_, ha, rfl‚ü©
  haveI := hs.fintype
  haveI := Fintype.ofSurjective _ hsurj
  set f'' : ‚àÄ a, a ‚àà s.toFinset ‚Üí Œ≤ := fun a h ‚Ü¶ f a (by simpa using h)
  exact
    @Finset.inj_on_of_surj_on_of_card_le _ _ _ t.toFinset f''
      (fun a ha ‚Ü¶ by { rw [mem_toFinset] at ha ‚ä¢; exact hf a ha }) (by simpa)
      (by { rwa [‚Üê ncard_eq_toFinset_card', ‚Üê ncard_eq_toFinset_card'] }) a‚ÇÅ
      (by simpa) a‚ÇÇ (by simpa) (by simpa)

theorem ncard_coe {Œ± : Type*} (s : Set Œ±) :
    Set.ncard (Set.univ : Set (Set.Elem s)) = s.ncard := by simp

@[simp] lemma ncard_graphOn (s : Set Œ±) (f : Œ± ‚Üí Œ≤) : (s.graphOn f).ncard = s.ncard := by
  rw [‚Üê ncard_image_of_injOn fst_injOn_graph, image_fst_graphOn]

section Lattice

theorem ncard_union_add_ncard_inter (s t : Set Œ±) (hs : s.Finite := by toFinite_tac)
    (ht : t.Finite := by toFinite_tac) : (s ‚à™ t).ncard + (s ‚à© t).ncard = s.ncard + t.ncard := by
  to_encard_tac; rw [hs.cast_ncard_eq, ht.cast_ncard_eq, (hs.union ht).cast_ncard_eq,
    (hs.subset inter_subset_left).cast_ncard_eq, encard_union_add_encard_inter]

theorem ncard_inter_add_ncard_union (s t : Set Œ±) (hs : s.Finite := by toFinite_tac)
    (ht : t.Finite := by toFinite_tac) : (s ‚à© t).ncard + (s ‚à™ t).ncard = s.ncard + t.ncard := by
  rw [add_comm, ncard_union_add_ncard_inter _ _ hs ht]

theorem ncard_union_le (s t : Set Œ±) : (s ‚à™ t).ncard ‚â§ s.ncard + t.ncard := by
  obtain (h | h) := (s ‚à™ t).finite_or_infinite
  ¬∑ to_encard_tac
    rw [h.cast_ncard_eq, (h.subset subset_union_left).cast_ncard_eq,
      (h.subset subset_union_right).cast_ncard_eq]
    apply encard_union_le
  rw [h.ncard]
  apply zero_le

theorem ncard_union_eq (h : Disjoint s t) (hs : s.Finite := by toFinite_tac)
    (ht : t.Finite := by toFinite_tac) : (s ‚à™ t).ncard = s.ncard + t.ncard := by
  to_encard_tac
  rw [hs.cast_ncard_eq, ht.cast_ncard_eq, (hs.union ht).cast_ncard_eq, encard_union_eq h]

theorem ncard_union_eq_iff (hs : s.Finite := by toFinite_tac)
    (ht : t.Finite := by toFinite_tac) : (s ‚à™ t).ncard = s.ncard + t.ncard ‚Üî Disjoint s t := by
  rw [‚Üê ncard_union_add_ncard_inter s t hs ht, left_eq_add,
    ncard_eq_zero (hs.inter_of_left t), disjoint_iff_inter_eq_empty]

theorem ncard_union_lt (hs : s.Finite := by toFinite_tac)
    (ht : t.Finite := by toFinite_tac) (h : ¬¨ Disjoint s t) :
    (s ‚à™ t).ncard < s.ncard + t.ncard :=
  (ncard_union_le s t).lt_of_ne (mt (ncard_union_eq_iff hs ht).mp h)

theorem ncard_diff_add_ncard_of_subset (h : s ‚äÜ t) (ht : t.Finite := by toFinite_tac) :
    (t \ s).ncard + s.ncard = t.ncard := by
  to_encard_tac
  rw [ht.cast_ncard_eq, (ht.subset h).cast_ncard_eq, ht.diff.cast_ncard_eq,
    encard_diff_add_encard_of_subset h]

theorem ncard_diff (hst : s ‚äÜ t) (hs : s.Finite := by toFinite_tac) :
    (t \ s).ncard = t.ncard - s.ncard := by
  obtain ht | ht := t.finite_or_infinite
  ¬∑ rw [‚Üê ncard_diff_add_ncard_of_subset hst ht, add_tsub_cancel_right]
  ¬∑ rw [ht.ncard, Nat.zero_sub, (ht.diff hs).ncard]

lemma cast_ncard_sdiff {R : Type*} [AddGroupWithOne R] (hst : s ‚äÜ t) (ht : t.Finite) :
    ((t \ s).ncard : R) = t.ncard - s.ncard := by
  rw [ncard_diff hst (ht.subset hst), Nat.cast_sub (ncard_le_ncard hst ht)]

theorem ncard_le_ncard_diff_add_ncard (s t : Set Œ±) (ht : t.Finite := by toFinite_tac) :
    s.ncard ‚â§ (s \ t).ncard + t.ncard := by
  rcases s.finite_or_infinite with hs | hs
  ¬∑ to_encard_tac
    rw [ht.cast_ncard_eq, hs.cast_ncard_eq, hs.diff.cast_ncard_eq]
    apply encard_le_encard_diff_add_encard
  convert Nat.zero_le _
  rw [hs.ncard]

theorem le_ncard_diff (s t : Set Œ±) (hs : s.Finite := by toFinite_tac) :
    t.ncard - s.ncard ‚â§ (t \ s).ncard :=
  tsub_le_iff_left.mpr (by rw [add_comm]; apply ncard_le_ncard_diff_add_ncard _ _ hs)

theorem ncard_diff_add_ncard (s t : Set Œ±) (hs : s.Finite := by toFinite_tac)
    (ht : t.Finite := by toFinite_tac) :
    (s \ t).ncard + t.ncard = (s ‚à™ t).ncard := by
  rw [‚Üê ncard_union_eq disjoint_sdiff_left hs.diff ht, diff_union_self]

theorem diff_nonempty_of_ncard_lt_ncard (h : s.ncard < t.ncard) (hs : s.Finite := by toFinite_tac) :
    (t \ s).Nonempty := by
  rw [Set.nonempty_iff_ne_empty, Ne, diff_eq_empty]
  exact fun h' ‚Ü¶ h.not_ge (ncard_le_ncard h' hs)

theorem exists_mem_notMem_of_ncard_lt_ncard (h : s.ncard < t.ncard)
    (hs : s.Finite := by toFinite_tac) : ‚àÉ e, e ‚àà t ‚àß e ‚àâ s :=
  diff_nonempty_of_ncard_lt_ncard h hs

@[deprecated (since := "2025-05-23")]
alias exists_mem_not_mem_of_ncard_lt_ncard := exists_mem_notMem_of_ncard_lt_ncard

@[simp] theorem ncard_inter_add_ncard_diff_eq_ncard (s t : Set Œ±)
    (hs : s.Finite := by toFinite_tac) : (s ‚à© t).ncard + (s \ t).ncard = s.ncard := by
  rw [‚Üê ncard_union_eq (disjoint_of_subset_left inter_subset_right disjoint_sdiff_right)
    (hs.inter_of_left _) hs.diff, union_comm, diff_union_inter]

theorem ncard_eq_ncard_iff_ncard_diff_eq_ncard_diff (hs : s.Finite := by toFinite_tac)
    (ht : t.Finite := by toFinite_tac) : s.ncard = t.ncard ‚Üî (s \ t).ncard = (t \ s).ncard := by
  rw [‚Üê ncard_inter_add_ncard_diff_eq_ncard s t hs, ‚Üê ncard_inter_add_ncard_diff_eq_ncard t s ht,
    inter_comm, add_right_inj]

theorem ncard_le_ncard_iff_ncard_diff_le_ncard_diff (hs : s.Finite := by toFinite_tac)
    (ht : t.Finite := by toFinite_tac) : s.ncard ‚â§ t.ncard ‚Üî (s \ t).ncard ‚â§ (t \ s).ncard := by
  rw [‚Üê ncard_inter_add_ncard_diff_eq_ncard s t hs, ‚Üê ncard_inter_add_ncard_diff_eq_ncard t s ht,
    inter_comm, add_le_add_iff_left]

theorem ncard_lt_ncard_iff_ncard_diff_lt_ncard_diff (hs : s.Finite := by toFinite_tac)
    (ht : t.Finite := by toFinite_tac) : s.ncard < t.ncard ‚Üî (s \ t).ncard < (t \ s).ncard := by
  rw [‚Üê ncard_inter_add_ncard_diff_eq_ncard s t hs, ‚Üê ncard_inter_add_ncard_diff_eq_ncard t s ht,
    inter_comm, add_lt_add_iff_left]

theorem ncard_add_ncard_compl (s : Set Œ±) (hs : s.Finite := by toFinite_tac)
    (hsc : s·∂ú.Finite := by toFinite_tac) : s.ncard + s·∂ú.ncard = Nat.card Œ± := by
  rw [‚Üê ncard_univ, ‚Üê ncard_union_eq (@disjoint_compl_right _ _ s) hs hsc, union_compl_self]

theorem eq_univ_iff_ncard [Finite Œ±] (s : Set Œ±) :
    s = univ ‚Üî ncard s = Nat.card Œ± := by
  rw [‚Üê compl_empty_iff, ‚Üê ncard_eq_zero, ‚Üê ncard_add_ncard_compl s, left_eq_add]

lemma even_ncard_compl_iff [Finite Œ±] (heven : Even (Nat.card Œ±)) (s : Set Œ±) :
    Even s·∂ú.ncard ‚Üî Even s.ncard := by
  rwa [iff_comm, ‚Üê Nat.even_add, ncard_add_ncard_compl]

lemma odd_ncard_compl_iff [Finite Œ±] (heven : Even (Nat.card Œ±)) (s : Set Œ±) :
    Odd s·∂ú.ncard ‚Üî Odd s.ncard := by
  rw [‚Üê Nat.not_even_iff_odd, even_ncard_compl_iff heven, Nat.not_even_iff_odd]

theorem nonempty_inter_of_lt_ncard_add_ncard [Finite Œ±]
    (h : Nat.card Œ± < s.ncard + t.ncard) : (s ‚à© t).Nonempty := by
  rw [‚Üê ncard_union_add_ncard_inter s t] at h
  replace h := (s ‚à™ t).ncard_le_card.trans_lt h
  rwa [lt_add_iff_pos_right, ncard_pos] at h

theorem nonempty_inter_of_le_ncard_add_ncard [Finite Œ±]
    (h' : Nat.card Œ± ‚â§ s.ncard + t.ncard) (h : s ‚à™ t ‚â† univ) :
    (s ‚à© t).Nonempty := by
  rw [‚Üê ncard_union_add_ncard_inter s t] at h'
  replace h := (ncard_lt_card h).trans_le h'
  rwa [lt_add_iff_pos_right, ncard_pos] at h

theorem union_ne_univ_of_ncard_add_ncard_lt
    (h : s.ncard + t.ncard < Nat.card Œ±) : s ‚à™ t ‚â† univ := by
  contrapose! h
  rw [‚Üê ncard_univ, ‚Üê h]
  exact ncard_union_le s t

theorem nonempty_inter_compl_of_ncard_add_ncard_lt
    (h : s.ncard + t.ncard < Nat.card Œ±) : (s·∂ú ‚à© t·∂ú).Nonempty := by
  rw [‚Üê compl_union, nonempty_compl]
  exact union_ne_univ_of_ncard_add_ncard_lt h

end Lattice

/-- Given a subset `s` of a set `t`, of sizes at most and at least `n` respectively, there exists a
set `u` of size `n` which is both a superset of `s` and a subset of `t`. -/
lemma exists_subsuperset_card_eq {n : ‚Ñï} (hst : s ‚äÜ t) (hsn : s.ncard ‚â§ n) (hnt : n ‚â§ t.ncard) :
    ‚àÉ u, s ‚äÜ u ‚àß u ‚äÜ t ‚àß u.ncard = n := by
  obtain ht | ht := t.infinite_or_finite
  ¬∑ rw [ht.ncard, Nat.le_zero, ‚Üê ht.ncard] at hnt
    exact ‚ü®t, hst, Subset.rfl, hnt.symm‚ü©
  lift s to Finset Œ± using ht.subset hst
  lift t to Finset Œ± using ht
  obtain ‚ü®u, hsu, hut, hu‚ü© := Finset.exists_subsuperset_card_eq (mod_cast hst) (by simpa using hsn)
    (mod_cast hnt)
  exact ‚ü®u, mod_cast hsu, mod_cast hut, mod_cast hu‚ü©

/-- We can shrink a set to any smaller size. -/
lemma exists_subset_card_eq {n : ‚Ñï} (hns : n ‚â§ s.ncard) : ‚àÉ t ‚äÜ s, t.ncard = n := by
  simpa using exists_subsuperset_card_eq s.empty_subset (by simp) hns

theorem Infinite.exists_subset_ncard_eq {s : Set Œ±} (hs : s.Infinite) (k : ‚Ñï) :
    ‚àÉ t, t ‚äÜ s ‚àß t.Finite ‚àß t.ncard = k := by
  have := hs.to_subtype
  obtain ‚ü®t', -, rfl‚ü© := @Infinite.exists_subset_card_eq s univ infinite_univ k
  refine ‚ü®Subtype.val '' (t' : Set s), by simp, Finite.image _ (by simp), ?_‚ü©
  rw [ncard_image_of_injective _ Subtype.coe_injective]
  simp

theorem Infinite.exists_superset_ncard_eq {s t : Set Œ±} (ht : t.Infinite) (hst : s ‚äÜ t)
    (hs : s.Finite) {k : ‚Ñï} (hsk : s.ncard ‚â§ k) : ‚àÉ s', s ‚äÜ s' ‚àß s' ‚äÜ t ‚àß s'.ncard = k := by
  obtain ‚ü®s‚ÇÅ, hs‚ÇÅ, hs‚ÇÅfin, hs‚ÇÅcard‚ü© := (ht.diff hs).exists_subset_ncard_eq (k - s.ncard)
  refine ‚ü®s ‚à™ s‚ÇÅ, subset_union_left, union_subset hst (hs‚ÇÅ.trans diff_subset), ?_‚ü©
  rwa [ncard_union_eq (disjoint_of_subset_right hs‚ÇÅ disjoint_sdiff_right) hs hs‚ÇÅfin, hs‚ÇÅcard,
    add_tsub_cancel_of_le]

theorem exists_subset_or_subset_of_two_mul_lt_ncard {n : ‚Ñï} (hst : 2 * n < (s ‚à™ t).ncard) :
    ‚àÉ r : Set Œ±, n < r.ncard ‚àß (r ‚äÜ s ‚à® r ‚äÜ t) := by
  classical
  have hu := finite_of_ncard_ne_zero ((Nat.zero_le _).trans_lt hst).ne.symm
  rw [ncard_eq_toFinset_card _ hu,
    Finite.toFinset_union (hu.subset subset_union_left)
      (hu.subset subset_union_right)] at hst
  obtain ‚ü®r', hnr', hr'‚ü© := Finset.exists_subset_or_subset_of_two_mul_lt_card hst
  exact ‚ü®r', by simpa, by simpa using hr'‚ü©

lemma _root_.Finset.exists_not_mem_of_card_lt_enatCard {s : Finset Œ±} (hs : s.card < ENat.card Œ±) :
    ‚àÉ a, a ‚àâ s := by
  contrapose! hs; simp [‚Üê Set.encard_coe_eq_coe_finsetCard, Set.eq_univ_of_forall (s := s.toSet) hs]

/-! ### Explicit description of a set from its cardinality -/

@[simp] theorem ncard_eq_one : s.ncard = 1 ‚Üî ‚àÉ a, s = {a} := by
  refine ‚ü®fun h ‚Ü¶ ?_, by rintro ‚ü®a, rfl‚ü©; rw [ncard_singleton]‚ü©
  have hft := (finite_of_ncard_ne_zero (ne_zero_of_eq_one h)).fintype
  simp_rw [ncard_eq_toFinset_card', @Finset.card_eq_one _ (toFinset s)] at h
  refine h.imp fun a ha ‚Ü¶ ?_
  simp_rw [Set.ext_iff, mem_singleton_iff]
  simp only [Finset.ext_iff, mem_toFinset, Finset.mem_singleton] at ha
  exact ha

theorem exists_eq_insert_iff_ncard (hs : s.Finite := by toFinite_tac) :
    (‚àÉ a ‚àâ s, insert a s = t) ‚Üî s ‚äÜ t ‚àß s.ncard + 1 = t.ncard := by
  classical
  rcases t.finite_or_infinite with ht | ht
  ¬∑ rw [ncard_eq_toFinset_card _ hs, ncard_eq_toFinset_card _ ht,
      ‚Üê @Finite.toFinset_subset_toFinset _ _ _ hs ht, ‚Üê Finset.exists_eq_insert_iff]
    convert Iff.rfl using 2; simp only [Finite.mem_toFinset]
    ext x
    simp [Finset.ext_iff, Set.ext_iff]
  simp only [ht.ncard, add_eq_zero, and_false, iff_false, not_exists, not_and,
    reduceCtorEq]
  rintro x - rfl
  exact ht (hs.insert x)

theorem ncard_le_one (hs : s.Finite := by toFinite_tac) :
    s.ncard ‚â§ 1 ‚Üî ‚àÄ a ‚àà s, ‚àÄ b ‚àà s, a = b := by
  simp_rw [ncard_eq_toFinset_card _ hs, Finset.card_le_one, Finite.mem_toFinset]

@[simp] theorem ncard_le_one_iff_subsingleton [Finite s] :
    s.ncard ‚â§ 1 ‚Üî s.Subsingleton :=
  ncard_le_one <| inferInstanceAs (Finite s)

theorem ncard_le_one_iff (hs : s.Finite := by toFinite_tac) :
    s.ncard ‚â§ 1 ‚Üî ‚àÄ {a b}, a ‚àà s ‚Üí b ‚àà s ‚Üí a = b := by
  rw [ncard_le_one hs]
  tauto

theorem ncard_le_one_iff_eq (hs : s.Finite := by toFinite_tac) :
    s.ncard ‚â§ 1 ‚Üî s = ‚àÖ ‚à® ‚àÉ a, s = {a} := by
  obtain rfl | ‚ü®x, hx‚ü© := s.eq_empty_or_nonempty
  ¬∑ exact iff_of_true (by simp) (Or.inl rfl)
  rw [ncard_le_one_iff hs]
  refine ‚ü®fun h ‚Ü¶ Or.inr ‚ü®x, (singleton_subset_iff.mpr hx).antisymm' fun y hy ‚Ü¶ h hy hx‚ü©, ?_‚ü©
  grind

theorem ncard_le_one_iff_subset_singleton [Nonempty Œ±]
    (hs : s.Finite := by toFinite_tac) :
    s.ncard ‚â§ 1 ‚Üî ‚àÉ x : Œ±, s ‚äÜ {x} := by
  simp_rw [ncard_eq_toFinset_card _ hs, Finset.card_le_one_iff_subset_singleton,
    Finite.toFinset_subset, Finset.coe_singleton]

/-- A `Set` of a subsingleton type has cardinality at most one. -/
theorem ncard_le_one_of_subsingleton [Subsingleton Œ±] (s : Set Œ±) : s.ncard ‚â§ 1 := by
  rw [ncard_eq_toFinset_card]
  exact Finset.card_le_one_of_subsingleton _

theorem one_lt_ncard (hs : s.Finite := by toFinite_tac) :
    1 < s.ncard ‚Üî ‚àÉ a ‚àà s, ‚àÉ b ‚àà s, a ‚â† b := by
  simp_rw [ncard_eq_toFinset_card _ hs, Finset.one_lt_card, Finite.mem_toFinset]

theorem one_lt_ncard_iff (hs : s.Finite := by toFinite_tac) :
    1 < s.ncard ‚Üî ‚àÉ a b, a ‚àà s ‚àß b ‚àà s ‚àß a ‚â† b := by
  rw [one_lt_ncard hs]
  simp only [exists_and_left]

lemma one_lt_ncard_of_nonempty_of_even (hs : Set.Finite s) (hn : Set.Nonempty s := by toFinite_tac)
    (he : Even (s.ncard)) : 1 < s.ncard := by
  rw [‚Üê Set.ncard_pos hs] at hn
  have : s.ncard ‚â† 1 := fun h ‚Ü¶ by simp [h] at he
  cutsat

theorem two_lt_ncard_iff (hs : s.Finite := by toFinite_tac) :
    2 < s.ncard ‚Üî ‚àÉ a b c, a ‚àà s ‚àß b ‚àà s ‚àß c ‚àà s ‚àß a ‚â† b ‚àß a ‚â† c ‚àß b ‚â† c := by
  simp_rw [ncard_eq_toFinset_card _ hs, Finset.two_lt_card_iff, Finite.mem_toFinset]

theorem two_lt_ncard (hs : s.Finite := by toFinite_tac) :
    2 < s.ncard ‚Üî ‚àÉ a ‚àà s, ‚àÉ b ‚àà s, ‚àÉ c ‚àà s, a ‚â† b ‚àß a ‚â† c ‚àß b ‚â† c := by
  simp only [two_lt_ncard_iff hs, exists_and_left]

theorem three_lt_ncard_iff (hs : s.Finite := by toFinite_tac) :
    3 < s.ncard ‚Üî
    ‚àÉ a b c d, a ‚àà s ‚àß b ‚àà s ‚àß c ‚àà s ‚àß d ‚àà s ‚àß a ‚â† b ‚àß a ‚â† c ‚àß a ‚â† d ‚àß b ‚â† c ‚àß b ‚â† d ‚àß c ‚â† d := by
  simp_rw [ncard_eq_toFinset_card _ hs, Finset.three_lt_card_iff, Finite.mem_toFinset]

theorem three_lt_ncard (hs : s.Finite := by toFinite_tac) :
    3 < s.ncard ‚Üî
    ‚àÉ a ‚àà s, ‚àÉ b ‚àà s, ‚àÉ c ‚àà s, ‚àÉ d ‚àà s, a ‚â† b ‚àß a ‚â† c ‚àß a ‚â† d ‚àß b ‚â† c ‚àß b ‚â† d ‚àß c ‚â† d := by
  simp only [three_lt_ncard_iff hs, exists_and_left]

theorem exists_ne_of_one_lt_ncard (hs : 1 < s.ncard) (a : Œ±) : ‚àÉ b, b ‚àà s ‚àß b ‚â† a := by
  have hsf := finite_of_ncard_ne_zero (zero_lt_one.trans hs).ne.symm
  rw [ncard_eq_toFinset_card _ hsf] at hs
  simpa only [Finite.mem_toFinset] using Finset.exists_mem_ne hs a

theorem eq_insert_of_ncard_eq_succ {n : ‚Ñï} (h : s.ncard = n + 1) :
    ‚àÉ a t, a ‚àâ t ‚àß insert a t = s ‚àß t.ncard = n := by
  classical
  have hsf := finite_of_ncard_pos (n.zero_lt_succ.trans_eq h.symm)
  rw [ncard_eq_toFinset_card _ hsf, Finset.card_eq_succ] at h
  obtain ‚ü®a, t, hat, hts, rfl‚ü© := h
  simp only [Finset.ext_iff, Finset.mem_insert, Finite.mem_toFinset] at hts
  refine ‚ü®a, t, hat, ?_, ?_‚ü©
  ¬∑ simp [Set.ext_iff, hts]
  ¬∑ simp

theorem ncard_eq_succ {n : ‚Ñï} (hs : s.Finite := by toFinite_tac) :
    s.ncard = n + 1 ‚Üî ‚àÉ a t, a ‚àâ t ‚àß insert a t = s ‚àß t.ncard = n := by
  refine ‚ü®eq_insert_of_ncard_eq_succ, ?_‚ü©
  rintro ‚ü®a, t, hat, h, rfl‚ü©
  rw [‚Üê h, ncard_insert_of_notMem hat (hs.subset ((subset_insert a t).trans_eq h))]

theorem ncard_eq_two : s.ncard = 2 ‚Üî ‚àÉ x y, x ‚â† y ‚àß s = {x, y} := by
  rw [‚Üê encard_eq_two, ncard_def]
  simp

theorem ncard_eq_three : s.ncard = 3 ‚Üî ‚àÉ x y z, x ‚â† y ‚àß x ‚â† z ‚àß y ‚â† z ‚àß s = {x, y, z} := by
  rw [‚Üê encard_eq_three, ncard_def]
  simp

theorem ncard_eq_four : s.ncard = 4 ‚Üî
    ‚àÉ x y z w, x ‚â† y ‚àß x ‚â† z ‚àß x ‚â† w ‚àß y ‚â† z ‚àß y ‚â† w ‚àß z ‚â† w ‚àß s = {x, y, z, w} := by
  rw [‚Üê encard_eq_four, ncard_def]
  simp

end ncard
end Set
