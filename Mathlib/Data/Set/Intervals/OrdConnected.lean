/-
Copyright (c) 2020 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import Mathlib.Data.Set.Intervals.UnorderedInterval
import Mathlib.Data.Set.Lattice
import Mathlib.Order.Antichain

#align_import data.set.intervals.ord_connected from "leanprover-community/mathlib"@"76de8ae01554c3b37d66544866659ff174e66e1f"

/-!
# Order-connected sets

We say that a set `s : Set Î±` is `OrdConnected` if for all `x y âˆˆ s` it includes the
interval `[[x, y]]`. If `Î±` is a `DenselyOrdered` `ConditionallyCompleteLinearOrder` with
the `OrderTopology`, then this condition is equivalent to `IsPreconnected s`. If `Î±` is a
`LinearOrderedField`, then this condition is also equivalent to `Convex Î± s`.

In this file we prove that intersection of a family of `OrdConnected` sets is `OrdConnected` and
that all standard intervals are `OrdConnected`.
-/

open Interval

open OrderDual (toDual ofDual)

namespace Set

section Preorder

variable {Î± Î² : Type*} [Preorder Î±] [Preorder Î²] {s t : Set Î±}

/-- We say that a set `s : Set Î±` is `OrdConnected` if for all `x y âˆˆ s` it includes the
interval `[[x, y]]`. If `Î±` is a `DenselyOrdered` `ConditionallyCompleteLinearOrder` with
the `OrderTopology`, then this condition is equivalent to `IsPreconnected s`. If `Î±` is a
`LinearOrderedField`, then this condition is also equivalent to `Convex Î± s`. -/
class OrdConnected (s : Set Î±) : Prop where
  /-- `s : Set Î±` is `OrdConnected` if for all `x y âˆˆ s` it includes the interval `[[x, y]]`. -/
  out' â¦ƒxâ¦„ (hx : x âˆˆ s) â¦ƒyâ¦„ (hy : y âˆˆ s) : Icc x y âŠ† s
#align set.ord_connected Set.OrdConnected

theorem OrdConnected.out (h : OrdConnected s) : âˆ€ â¦ƒxâ¦„ (_ : x âˆˆ s) â¦ƒyâ¦„ (_ : y âˆˆ s), Icc x y âŠ† s :=
  h.1
#align set.ord_connected.out Set.OrdConnected.out

theorem ordConnected_def : OrdConnected s â†” âˆ€ â¦ƒxâ¦„ (_ : x âˆˆ s) â¦ƒyâ¦„ (_ : y âˆˆ s), Icc x y âŠ† s :=
  âŸ¨fun h => h.1, fun h => âŸ¨hâŸ©âŸ©
#align set.ord_connected_def Set.ordConnected_def

/-- It suffices to prove `[[x, y]] âŠ† s` for `x y âˆˆ s`, `x â‰¤ y`. -/
theorem ordConnected_iff : OrdConnected s â†” âˆ€ x âˆˆ s, âˆ€ y âˆˆ s, x â‰¤ y â†’ Icc x y âŠ† s :=
  ordConnected_def.trans
    âŸ¨fun hs _ hx _ hy _ => hs hx hy, fun H x hx y hy _ hz => H x hx y hy (le_trans hz.1 hz.2) hzâŸ©
#align set.ord_connected_iff Set.ordConnected_iff

theorem ordConnected_of_Ioo {Î± : Type*} [PartialOrder Î±] {s : Set Î±}
    (hs : âˆ€ x âˆˆ s, âˆ€ y âˆˆ s, x < y â†’ Ioo x y âŠ† s) : OrdConnected s := by
  rw [ordConnected_iff]
  -- âŠ¢ âˆ€ (x : Î±), x âˆˆ s â†’ âˆ€ (y : Î±), y âˆˆ s â†’ x â‰¤ y â†’ Icc x y âŠ† s
  intro x hx y hy hxy
  -- âŠ¢ Icc x y âŠ† s
  rcases eq_or_lt_of_le hxy with (rfl | hxy'); Â· simpa
  -- âŠ¢ Icc x x âŠ† s
                                                 -- ðŸŽ‰ no goals
  rw [â† Ioc_insert_left hxy, â† Ioo_insert_right hxy']
  -- âŠ¢ insert x (insert y (Ioo x y)) âŠ† s
  exact insert_subset_iff.2 âŸ¨hx, insert_subset_iff.2 âŸ¨hy, hs x hx y hy hxy'âŸ©âŸ©
  -- ðŸŽ‰ no goals
#align set.ord_connected_of_Ioo Set.ordConnected_of_Ioo

theorem OrdConnected.preimage_mono {f : Î² â†’ Î±} (hs : OrdConnected s) (hf : Monotone f) :
    OrdConnected (f â»Â¹' s) :=
  âŸ¨fun _ hx _ hy _ hz => hs.out hx hy âŸ¨hf hz.1, hf hz.2âŸ©âŸ©
#align set.ord_connected.preimage_mono Set.OrdConnected.preimage_mono

theorem OrdConnected.preimage_anti {f : Î² â†’ Î±} (hs : OrdConnected s) (hf : Antitone f) :
    OrdConnected (f â»Â¹' s) :=
  âŸ¨fun _ hx _ hy _ hz => hs.out hy hx âŸ¨hf hz.2, hf hz.1âŸ©âŸ©
#align set.ord_connected.preimage_anti Set.OrdConnected.preimage_anti

protected theorem Icc_subset (s : Set Î±) [hs : OrdConnected s] {x y} (hx : x âˆˆ s) (hy : y âˆˆ s) :
    Icc x y âŠ† s :=
  hs.out hx hy
#align set.Icc_subset Set.Icc_subset

theorem OrdConnected.inter {s t : Set Î±} (hs : OrdConnected s) (ht : OrdConnected t) :
    OrdConnected (s âˆ© t) :=
  âŸ¨fun _ hx _ hy => subset_inter (hs.out hx.1 hy.1) (ht.out hx.2 hy.2)âŸ©
#align set.ord_connected.inter Set.OrdConnected.inter

instance OrdConnected.inter' {s t : Set Î±} [OrdConnected s] [OrdConnected t] :
    OrdConnected (s âˆ© t) :=
  OrdConnected.inter â€¹_â€º â€¹_â€º
#align set.ord_connected.inter' Set.OrdConnected.inter'

theorem OrdConnected.dual {s : Set Î±} (hs : OrdConnected s) :
    OrdConnected (OrderDual.ofDual â»Â¹' s) :=
  âŸ¨fun _ hx _ hy _ hz => hs.out hy hx âŸ¨hz.2, hz.1âŸ©âŸ©
#align set.ord_connected.dual Set.OrdConnected.dual

theorem ordConnected_dual {s : Set Î±} : OrdConnected (OrderDual.ofDual â»Â¹' s) â†” OrdConnected s :=
  âŸ¨fun h => by simpa only [ordConnected_def] using h.dual, fun h => h.dualâŸ©
               -- ðŸŽ‰ no goals
#align set.ord_connected_dual Set.ordConnected_dual

theorem ordConnected_sInter {S : Set (Set Î±)} (hS : âˆ€ s âˆˆ S, OrdConnected s) :
    OrdConnected (â‹‚â‚€ S) :=
  âŸ¨fun _ hx _ hy => subset_sInter fun s hs => (hS s hs).out (hx s hs) (hy s hs)âŸ©
#align set.ord_connected_sInter Set.ordConnected_sInter

theorem ordConnected_iInter {Î¹ : Sort*} {s : Î¹ â†’ Set Î±} (hs : âˆ€ i, OrdConnected (s i)) :
    OrdConnected (â‹‚ i, s i) :=
  ordConnected_sInter <| forall_range_iff.2 hs
#align set.ord_connected_Inter Set.ordConnected_iInter

instance ordConnected_iInter' {Î¹ : Sort*} {s : Î¹ â†’ Set Î±} [âˆ€ i, OrdConnected (s i)] :
    OrdConnected (â‹‚ i, s i) :=
  ordConnected_iInter â€¹_â€º
#align set.ord_connected_Inter' Set.ordConnected_iInter'

/- ./././Mathport/Syntax/Translate/Expr.lean:107:6: warning: expanding binder group (i hi) -/
theorem ordConnected_biInter {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : âˆ€ (i : Î¹) (_ : p i), Set Î±}
    (hs : âˆ€ i hi, OrdConnected (s i hi)) : OrdConnected (â‹‚ (i) (hi), s i hi) :=
  ordConnected_iInter fun i => ordConnected_iInter <| hs i
#align set.ord_connected_bInter Set.ordConnected_biInter

theorem ordConnected_pi {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [âˆ€ i, Preorder (Î± i)] {s : Set Î¹}
    {t : âˆ€ i, Set (Î± i)} (h : âˆ€ i âˆˆ s, OrdConnected (t i)) : OrdConnected (s.pi t) :=
  âŸ¨fun _ hx _ hy _ hz i hi => (h i hi).out (hx i hi) (hy i hi) âŸ¨hz.1 i, hz.2 iâŸ©âŸ©
#align set.ord_connected_pi Set.ordConnected_pi

instance ordConnected_pi' {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [âˆ€ i, Preorder (Î± i)] {s : Set Î¹}
    {t : âˆ€ i, Set (Î± i)} [h : âˆ€ i, OrdConnected (t i)] : OrdConnected (s.pi t) :=
  ordConnected_pi fun i _ => h i
#align set.ord_connected_pi' Set.ordConnected_pi'

@[instance]
theorem ordConnected_Ici {a : Î±} : OrdConnected (Ici a) :=
  âŸ¨fun _ hx _ _ _ hz => le_trans hx hz.1âŸ©
#align set.ord_connected_Ici Set.ordConnected_Ici

@[instance]
theorem ordConnected_Iic {a : Î±} : OrdConnected (Iic a) :=
  âŸ¨fun _ _ _ hy _ hz => le_trans hz.2 hyâŸ©
#align set.ord_connected_Iic Set.ordConnected_Iic

@[instance]
theorem ordConnected_Ioi {a : Î±} : OrdConnected (Ioi a) :=
  âŸ¨fun _ hx _ _ _ hz => lt_of_lt_of_le hx hz.1âŸ©
#align set.ord_connected_Ioi Set.ordConnected_Ioi

@[instance]
theorem ordConnected_Iio {a : Î±} : OrdConnected (Iio a) :=
  âŸ¨fun _ _ _ hy _ hz => lt_of_le_of_lt hz.2 hyâŸ©
#align set.ord_connected_Iio Set.ordConnected_Iio

@[instance]
theorem ordConnected_Icc {a b : Î±} : OrdConnected (Icc a b) :=
  ordConnected_Ici.inter ordConnected_Iic
#align set.ord_connected_Icc Set.ordConnected_Icc

@[instance]
theorem ordConnected_Ico {a b : Î±} : OrdConnected (Ico a b) :=
  ordConnected_Ici.inter ordConnected_Iio
#align set.ord_connected_Ico Set.ordConnected_Ico

@[instance]
theorem ordConnected_Ioc {a b : Î±} : OrdConnected (Ioc a b) :=
  ordConnected_Ioi.inter ordConnected_Iic
#align set.ord_connected_Ioc Set.ordConnected_Ioc

@[instance]
theorem ordConnected_Ioo {a b : Î±} : OrdConnected (Ioo a b) :=
  ordConnected_Ioi.inter ordConnected_Iio
#align set.ord_connected_Ioo Set.ordConnected_Ioo

@[instance]
theorem ordConnected_singleton {Î± : Type*} [PartialOrder Î±] {a : Î±} :
    OrdConnected ({a} : Set Î±) := by
  rw [â† Icc_self]
  -- âŠ¢ OrdConnected (Icc a a)
  exact ordConnected_Icc
  -- ðŸŽ‰ no goals
#align set.ord_connected_singleton Set.ordConnected_singleton

@[instance]
theorem ordConnected_empty : OrdConnected (âˆ… : Set Î±) :=
  âŸ¨fun _ => False.elimâŸ©
#align set.ord_connected_empty Set.ordConnected_empty

@[instance]
theorem ordConnected_univ : OrdConnected (univ : Set Î±) :=
  âŸ¨fun _ _ _ _ => subset_univ _âŸ©
#align set.ord_connected_univ Set.ordConnected_univ

/-- In a dense order `Î±`, the subtype from an `OrdConnected` set is also densely ordered. -/
instance instDenselyOrdered [DenselyOrdered Î±] {s : Set Î±} [hs : OrdConnected s] :
    DenselyOrdered s :=
  âŸ¨fun a b (h : (a : Î±) < b) =>
    let âŸ¨x, HâŸ© := exists_between h
    âŸ¨âŸ¨x, (hs.out a.2 b.2) (Ioo_subset_Icc_self H)âŸ©, HâŸ©âŸ©

@[instance]
theorem ordConnected_preimage {F : Type*} [OrderHomClass F Î± Î²] (f : F) {s : Set Î²}
    [hs : OrdConnected s] : OrdConnected (f â»Â¹' s) :=
  âŸ¨fun _ hx _ hy _ hz => hs.out hx hy âŸ¨OrderHomClass.mono _ hz.1, OrderHomClass.mono _ hz.2âŸ©âŸ©
#align set.ord_connected_preimage Set.ordConnected_preimage

@[instance]
theorem ordConnected_image {E : Type*} [OrderIsoClass E Î± Î²] (e : E) {s : Set Î±}
    [hs : OrdConnected s] : OrdConnected (e '' s) := by
  erw [(e : Î± â‰ƒo Î²).image_eq_preimage]
  -- âŠ¢ OrdConnected (â†‘(OrderIso.symm â†‘e) â»Â¹' s)
  apply ordConnected_preimage (e : Î± â‰ƒo Î²).symm
  -- ðŸŽ‰ no goals
#align set.ord_connected_image Set.ordConnected_image

-- porting note: split up `simp_rw [â† image_univ, OrdConnected_image e]`, would not work otherwise
@[instance]
theorem ordConnected_range {E : Type*} [OrderIsoClass E Î± Î²] (e : E) : OrdConnected (range e) := by
  simp_rw [â† image_univ]
  -- âŠ¢ OrdConnected (â†‘e '' univ)
  exact ordConnected_image (e : Î± â‰ƒo Î²)
  -- ðŸŽ‰ no goals
#align set.ord_connected_range Set.ordConnected_range

@[simp]
theorem dual_ordConnected_iff {s : Set Î±} : OrdConnected (ofDual â»Â¹' s) â†” OrdConnected s := by
  simp_rw [ordConnected_def, toDual.surjective.forall, dual_Icc, Subtype.forall']
  -- âŠ¢ (âˆ€ (x x_1 : { a // â†‘toDual a âˆˆ â†‘ofDual â»Â¹' s }), â†‘ofDual â»Â¹' Icc â†‘x_1 â†‘x âŠ† â†‘ â€¦
  exact forall_swap
  -- ðŸŽ‰ no goals
#align set.dual_ord_connected_iff Set.dual_ordConnected_iff

@[instance]
theorem dual_ordConnected {s : Set Î±} [OrdConnected s] : OrdConnected (ofDual â»Â¹' s) :=
  dual_ordConnected_iff.2 â€¹_â€º
#align set.dual_ord_connected Set.dual_ordConnected

end Preorder

section PartialOrder

variable {Î± : Type*} [PartialOrder Î±] {s : Set Î±}

protected theorem _root_.IsAntichain.ordConnected (hs : IsAntichain (Â· â‰¤ Â·) s) : s.OrdConnected :=
  âŸ¨fun x hx y hy z hz => by
    obtain rfl := hs.eq hx hy (hz.1.trans hz.2)
    -- âŠ¢ z âˆˆ s
    rw [Icc_self, mem_singleton_iff] at hz
    -- âŠ¢ z âˆˆ s
    rwa [hz]âŸ©
    -- ðŸŽ‰ no goals
#align is_antichain.ord_connected IsAntichain.ordConnected

end PartialOrder

section LinearOrder

variable {Î± : Type*} [LinearOrder Î±] {s : Set Î±} {x : Î±}

@[instance]
theorem ordConnected_uIcc {a b : Î±} : OrdConnected [[a, b]] :=
  ordConnected_Icc
#align set.ord_connected_uIcc Set.ordConnected_uIcc

@[instance]
theorem ordConnected_uIoc {a b : Î±} : OrdConnected (Î™ a b) :=
  ordConnected_Ioc
#align set.ord_connected_uIoc Set.ordConnected_uIoc

theorem OrdConnected.uIcc_subset (hs : OrdConnected s) â¦ƒxâ¦„ (hx : x âˆˆ s) â¦ƒyâ¦„ (hy : y âˆˆ s) :
    [[x, y]] âŠ† s :=
  hs.out (min_rec' (Â· âˆˆ s) hx hy) (max_rec' (Â· âˆˆ s) hx hy)
#align set.ord_connected.uIcc_subset Set.OrdConnected.uIcc_subset

theorem OrdConnected.uIoc_subset (hs : OrdConnected s) â¦ƒxâ¦„ (hx : x âˆˆ s) â¦ƒyâ¦„ (hy : y âˆˆ s) :
    Î™ x y âŠ† s :=
  Ioc_subset_Icc_self.trans <| hs.uIcc_subset hx hy
#align set.ord_connected.uIoc_subset Set.OrdConnected.uIoc_subset

theorem ordConnected_iff_uIcc_subset :
    OrdConnected s â†” âˆ€ â¦ƒxâ¦„ (_ : x âˆˆ s) â¦ƒyâ¦„ (_ : y âˆˆ s), [[x, y]] âŠ† s :=
  âŸ¨fun h => h.uIcc_subset, fun H => âŸ¨fun _ hx _ hy => Icc_subset_uIcc.trans <| H hx hyâŸ©âŸ©
#align set.ord_connected_iff_uIcc_subset Set.ordConnected_iff_uIcc_subset

theorem ordConnected_of_uIcc_subset_left (h : âˆ€ y âˆˆ s, [[x, y]] âŠ† s) : OrdConnected s :=
  ordConnected_iff_uIcc_subset.2 fun y hy z hz =>
    calc
      [[y, z]] âŠ† [[y, x]] âˆª [[x, z]] := uIcc_subset_uIcc_union_uIcc
      _ = [[x, y]] âˆª [[x, z]] := by rw [uIcc_comm]
                                    -- ðŸŽ‰ no goals
      _ âŠ† s := union_subset (h y hy) (h z hz)
#align set.ord_connected_of_uIcc_subset_left Set.ordConnected_of_uIcc_subset_left

theorem ordConnected_iff_uIcc_subset_left (hx : x âˆˆ s) :
    OrdConnected s â†” âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ [[x, y]] âŠ† s :=
  âŸ¨fun hs => hs.uIcc_subset hx, ordConnected_of_uIcc_subset_leftâŸ©
#align set.ord_connected_iff_uIcc_subset_left Set.ordConnected_iff_uIcc_subset_left

theorem ordConnected_iff_uIcc_subset_right (hx : x âˆˆ s) :
    OrdConnected s â†” âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ [[y, x]] âŠ† s := by
  simp_rw [ordConnected_iff_uIcc_subset_left hx, uIcc_comm]
  -- ðŸŽ‰ no goals
#align set.ord_connected_iff_uIcc_subset_right Set.ordConnected_iff_uIcc_subset_right
