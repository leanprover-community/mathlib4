/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Data.Set.Intervals.OrdConnected

#align_import data.set.intervals.ord_connected_component from "leanprover-community/mathlib"@"92ca63f0fb391a9ca5f22d2409a6080e786d99f7"

/-!
# Order connected components of a set

In this file we define `Set.ordConnectedComponent s x` to be the set of `y` such that
`Set.uIcc x y ‚äÜ s` and prove some basic facts about this definition. At the moment of writing,
this construction is used only to prove that any linear order with order topology is a T‚ÇÖ space,
so we only add API needed for this lemma.
-/


open Interval Function OrderDual

namespace Set

variable {Œ± : Type*} [LinearOrder Œ±] {s t : Set Œ±} {x y z : Œ±}

/-- Order-connected component of a point `x` in a set `s`. It is defined as the set of `y` such that
`Set.uIcc x y ‚äÜ s`. Note that it is empty if and only if `x ‚àâ s`. -/
def ordConnectedComponent (s : Set Œ±) (x : Œ±) : Set Œ± :=
  { y | [[x, y]] ‚äÜ s }
#align set.ord_connected_component Set.ordConnectedComponent

theorem mem_ordConnectedComponent : y ‚àà ordConnectedComponent s x ‚Üî [[x, y]] ‚äÜ s :=
  Iff.rfl
#align set.mem_ord_connected_component Set.mem_ordConnectedComponent

theorem dual_ordConnectedComponent :
    ordConnectedComponent (ofDual ‚Åª¬π' s) (toDual x) = ofDual ‚Åª¬π' ordConnectedComponent s x :=
  ext <| (Surjective.forall toDual.surjective).2 fun x => by
    rw [mem_ordConnectedComponent, dual_uIcc]
    -- ‚ä¢ ‚ÜëofDual ‚Åª¬π' [[x‚úù, x]] ‚äÜ ‚ÜëofDual ‚Åª¬π' s ‚Üî ‚ÜëtoDual x ‚àà ‚ÜëofDual ‚Åª¬π' ordConnected ‚Ä¶
    rfl
    -- üéâ no goals
#align set.dual_ord_connected_component Set.dual_ordConnectedComponent

theorem ordConnectedComponent_subset : ordConnectedComponent s x ‚äÜ s := fun _ hy =>
  hy right_mem_uIcc
#align set.ord_connected_component_subset Set.ordConnectedComponent_subset

theorem subset_ordConnectedComponent {t} [h : OrdConnected s] (hs : x ‚àà s) (ht : s ‚äÜ t) :
    s ‚äÜ ordConnectedComponent t x := fun _ hy => (h.uIcc_subset hs hy).trans ht
#align set.subset_ord_connected_component Set.subset_ordConnectedComponent

@[simp]
theorem self_mem_ordConnectedComponent : x ‚àà ordConnectedComponent s x ‚Üî x ‚àà s := by
  rw [mem_ordConnectedComponent, uIcc_self, singleton_subset_iff]
  -- üéâ no goals
#align set.self_mem_ord_connected_component Set.self_mem_ordConnectedComponent

@[simp]
theorem nonempty_ordConnectedComponent : (ordConnectedComponent s x).Nonempty ‚Üî x ‚àà s :=
  ‚ü®fun ‚ü®_, hy‚ü© => hy <| left_mem_uIcc, fun h => ‚ü®x, self_mem_ordConnectedComponent.2 h‚ü©‚ü©
#align set.nonempty_ord_connected_component Set.nonempty_ordConnectedComponent

@[simp]
theorem ordConnectedComponent_eq_empty : ordConnectedComponent s x = ‚àÖ ‚Üî x ‚àâ s := by
  rw [‚Üê not_nonempty_iff_eq_empty, nonempty_ordConnectedComponent]
  -- üéâ no goals
#align set.ord_connected_component_eq_empty Set.ordConnectedComponent_eq_empty

@[simp]
theorem ordConnectedComponent_empty : ordConnectedComponent ‚àÖ x = ‚àÖ :=
  ordConnectedComponent_eq_empty.2 (not_mem_empty x)
#align set.ord_connected_component_empty Set.ordConnectedComponent_empty

@[simp]
theorem ordConnectedComponent_univ : ordConnectedComponent univ x = univ := by
  simp [ordConnectedComponent]
  -- üéâ no goals
#align set.ord_connected_component_univ Set.ordConnectedComponent_univ

theorem ordConnectedComponent_inter (s t : Set Œ±) (x : Œ±) :
    ordConnectedComponent (s ‚à© t) x = ordConnectedComponent s x ‚à© ordConnectedComponent t x := by
  simp [ordConnectedComponent, setOf_and]
  -- üéâ no goals
#align set.ord_connected_component_inter Set.ordConnectedComponent_inter

theorem mem_ordConnectedComponent_comm :
    y ‚àà ordConnectedComponent s x ‚Üî x ‚àà ordConnectedComponent s y := by
  rw [mem_ordConnectedComponent, mem_ordConnectedComponent, uIcc_comm]
  -- üéâ no goals
#align set.mem_ord_connected_component_comm Set.mem_ordConnectedComponent_comm

theorem mem_ordConnectedComponent_trans (hxy : y ‚àà ordConnectedComponent s x)
    (hyz : z ‚àà ordConnectedComponent s y) : z ‚àà ordConnectedComponent s x :=
  calc
    [[x, z]] ‚äÜ [[x, y]] ‚à™ [[y, z]] := uIcc_subset_uIcc_union_uIcc
    _ ‚äÜ s := union_subset hxy hyz
#align set.mem_ord_connected_component_trans Set.mem_ordConnectedComponent_trans

theorem ordConnectedComponent_eq (h : [[x, y]] ‚äÜ s) :
    ordConnectedComponent s x = ordConnectedComponent s y :=
  ext fun _ =>
    ‚ü®mem_ordConnectedComponent_trans (mem_ordConnectedComponent_comm.2 h),
      mem_ordConnectedComponent_trans h‚ü©
#align set.ord_connected_component_eq Set.ordConnectedComponent_eq

instance : OrdConnected (ordConnectedComponent s x) :=
  ordConnected_of_uIcc_subset_left fun _ hy _ hz => (uIcc_subset_uIcc_left hz).trans hy

/-- Projection from `s : Set Œ±` to `Œ±` sending each order connected component of `s` to a single
point of this component. -/
noncomputable def ordConnectedProj (s : Set Œ±) : s ‚Üí Œ± := fun x : s =>
  (nonempty_ordConnectedComponent.2 x.2).some
#align set.ord_connected_proj Set.ordConnectedProj

theorem ordConnectedProj_mem_ordConnectedComponent (s : Set Œ±) (x : s) :
    ordConnectedProj s x ‚àà ordConnectedComponent s x :=
  Nonempty.some_mem _
#align set.ord_connected_proj_mem_ord_connected_component Set.ordConnectedProj_mem_ordConnectedComponent

theorem mem_ordConnectedComponent_ordConnectedProj (s : Set Œ±) (x : s) :
    ‚Üëx ‚àà ordConnectedComponent s (ordConnectedProj s x) :=
  mem_ordConnectedComponent_comm.2 <| ordConnectedProj_mem_ordConnectedComponent s x
#align set.mem_ord_connected_component_ord_connected_proj Set.mem_ordConnectedComponent_ordConnectedProj

@[simp]
theorem ordConnectedComponent_ordConnectedProj (s : Set Œ±) (x : s) :
    ordConnectedComponent s (ordConnectedProj s x) = ordConnectedComponent s x :=
  ordConnectedComponent_eq <| mem_ordConnectedComponent_ordConnectedProj _ _
#align set.ord_connected_component_ord_connected_proj Set.ordConnectedComponent_ordConnectedProj

@[simp]
theorem ordConnectedProj_eq {x y : s} :
    ordConnectedProj s x = ordConnectedProj s y ‚Üî [[(x : Œ±), y]] ‚äÜ s := by
  constructor <;> intro h
  -- ‚ä¢ ordConnectedProj s x = ordConnectedProj s y ‚Üí [[‚Üëx, ‚Üëy]] ‚äÜ s
                  -- ‚ä¢ [[‚Üëx, ‚Üëy]] ‚äÜ s
                  -- ‚ä¢ ordConnectedProj s x = ordConnectedProj s y
  ¬∑ rw [‚Üê mem_ordConnectedComponent, ‚Üê ordConnectedComponent_ordConnectedProj, h,
      ordConnectedComponent_ordConnectedProj, self_mem_ordConnectedComponent]
    exact y.2
    -- üéâ no goals
  ¬∑ simp only [ordConnectedProj, ordConnectedComponent_eq h]
    -- üéâ no goals
#align set.ord_connected_proj_eq Set.ordConnectedProj_eq

/-- A set that intersects each order connected component of a set by a single point. Defined as the
range of `Set.ordConnectedProj s`. -/
def ordConnectedSection (s : Set Œ±) : Set Œ± :=
  range <| ordConnectedProj s
#align set.ord_connected_section Set.ordConnectedSection

theorem dual_ordConnectedSection (s : Set Œ±) :
    ordConnectedSection (ofDual ‚Åª¬π' s) = ofDual ‚Åª¬π' ordConnectedSection s := by
  simp_rw [ordConnectedSection, ordConnectedProj]
  -- ‚ä¢ (range fun x => Nonempty.some (_ : Set.Nonempty (ordConnectedComponent (‚ÜëofD ‚Ä¶
  ext x
  -- ‚ä¢ (x ‚àà range fun x => Nonempty.some (_ : Set.Nonempty (ordConnectedComponent ( ‚Ä¶
  simp [dual_ordConnectedComponent]
  -- ‚ä¢ (‚àÉ a h, Nonempty.some (_ : Set.Nonempty (‚ÜëofDual ‚Åª¬π' ordConnectedComponent s ‚Ä¶
  tauto
  -- üéâ no goals
#align set.dual_ord_connected_section Set.dual_ordConnectedSection

theorem ordConnectedSection_subset : ordConnectedSection s ‚äÜ s :=
  range_subset_iff.2 fun _ => ordConnectedComponent_subset <| Nonempty.some_mem _
#align set.ord_connected_section_subset Set.ordConnectedSection_subset

theorem eq_of_mem_ordConnectedSection_of_uIcc_subset (hx : x ‚àà ordConnectedSection s)
    (hy : y ‚àà ordConnectedSection s) (h : [[x, y]] ‚äÜ s) : x = y := by
  rcases hx with ‚ü®x, rfl‚ü©; rcases hy with ‚ü®y, rfl‚ü©
  -- ‚ä¢ ordConnectedProj s x = y
                           -- ‚ä¢ ordConnectedProj s x = ordConnectedProj s y
  exact
    ordConnectedProj_eq.2
      (mem_ordConnectedComponent_trans
        (mem_ordConnectedComponent_trans (ordConnectedProj_mem_ordConnectedComponent _ _) h)
        (mem_ordConnectedComponent_ordConnectedProj _ _))
#align set.eq_of_mem_ord_connected_section_of_uIcc_subset Set.eq_of_mem_ordConnectedSection_of_uIcc_subset

/-- Given two sets `s t : Set Œ±`, the set `Set.orderSeparatingSet s t` is the set of points that
belong both to some `Set.ordConnectedComponent t·∂ú x`, `x ‚àà s`, and to some
`Set.ordConnectedComponent s·∂ú x`, `x ‚àà t`. In the case of two disjoint closed sets, this is the
union of all open intervals $(a, b)$ such that their endpoints belong to different sets. -/
def ordSeparatingSet (s t : Set Œ±) : Set Œ± :=
  (‚ãÉ x ‚àà s, ordConnectedComponent t·∂ú x) ‚à© ‚ãÉ x ‚àà t, ordConnectedComponent s·∂ú x
#align set.ord_separating_set Set.ordSeparatingSet

theorem ordSeparatingSet_comm (s t : Set Œ±) : ordSeparatingSet s t = ordSeparatingSet t s :=
  inter_comm _ _
#align set.ord_separating_set_comm Set.ordSeparatingSet_comm

theorem disjoint_left_ordSeparatingSet : Disjoint s (ordSeparatingSet s t) :=
  Disjoint.inter_right' _ <|
    disjoint_iUnion‚ÇÇ_right.2 fun _ _ =>
      disjoint_compl_right.mono_right <| ordConnectedComponent_subset
#align set.disjoint_left_ord_separating_set Set.disjoint_left_ordSeparatingSet

theorem disjoint_right_ordSeparatingSet : Disjoint t (ordSeparatingSet s t) :=
  ordSeparatingSet_comm t s ‚ñ∏ disjoint_left_ordSeparatingSet
#align set.disjoint_right_ord_separating_set Set.disjoint_right_ordSeparatingSet

theorem dual_ordSeparatingSet :
    ordSeparatingSet (ofDual ‚Åª¬π' s) (ofDual ‚Åª¬π' t) = ofDual ‚Åª¬π' ordSeparatingSet s t := by
  simp only [ordSeparatingSet, mem_preimage, ‚Üê toDual.surjective.iUnion_comp, ofDual_toDual,
    dual_ordConnectedComponent, ‚Üê preimage_compl, preimage_inter, preimage_iUnion]
#align set.dual_ord_separating_set Set.dual_ordSeparatingSet

/-- An auxiliary neighborhood that will be used in the proof of `OrderTopology.t5Space`. -/
def ordT5Nhd (s t : Set Œ±) : Set Œ± :=
  ‚ãÉ x ‚àà s, ordConnectedComponent (t·∂ú ‚à© (ordConnectedSection <| ordSeparatingSet s t)·∂ú) x
#align set.ord_t5_nhd Set.ordT5Nhd

theorem disjoint_ordT5Nhd : Disjoint (ordT5Nhd s t) (ordT5Nhd t s) := by
  rw [disjoint_iff_inf_le]
  -- ‚ä¢ ordT5Nhd s t ‚äì ordT5Nhd t s ‚â§ ‚ä•
  rintro x ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©
  -- ‚ä¢ x ‚àà ‚ä•
  rcases mem_iUnion‚ÇÇ.1 hx‚ÇÅ with ‚ü®a, has, ha‚ü©
  -- ‚ä¢ x ‚àà ‚ä•
  clear hx‚ÇÅ
  -- ‚ä¢ x ‚àà ‚ä•
  rcases mem_iUnion‚ÇÇ.1 hx‚ÇÇ with ‚ü®b, hbt, hb‚ü©
  -- ‚ä¢ x ‚àà ‚ä•
  clear hx‚ÇÇ
  -- ‚ä¢ x ‚àà ‚ä•
  rw [mem_ordConnectedComponent, subset_inter_iff] at ha hb
  -- ‚ä¢ x ‚àà ‚ä•
  cases' le_total a b with hab hab
  -- ‚ä¢ x ‚àà ‚ä•
  on_goal 2 => swap_var a ‚Üî b, s ‚Üî t, ha ‚Üî hb, has ‚Üî hbt
  -- ‚ä¢ x ‚àà ‚ä•
  -- ‚ä¢ x ‚àà ‚ä•
  all_goals
-- porting note: wlog not implemented yet, the following replaces the three previous lines
-- wlog (discharger := tactic.skip) hab : a ‚â§ b := le_total a b using a b s t, b a t s
    cases' ha with ha ha'
    cases' hb with hb hb'
    have hsub : [[a, b]] ‚äÜ (ordSeparatingSet s t).ordConnectedSection·∂ú := by
      rw [ordSeparatingSet_comm, uIcc_comm] at hb'
      calc
        [[a, b]] ‚äÜ [[a, x]] ‚à™ [[x, b]] := uIcc_subset_uIcc_union_uIcc
        _ ‚äÜ (ordSeparatingSet s t).ordConnectedSection·∂ú := union_subset ha' hb'
    clear ha' hb'
    cases' le_total x a with hxa hax
    ¬∑ exact hb (Icc_subset_uIcc' ‚ü®hxa, hab‚ü©) has
    cases' le_total b x with hbx hxb
    ¬∑ exact ha (Icc_subset_uIcc ‚ü®hab, hbx‚ü©) hbt
    have h' : x ‚àà ordSeparatingSet s t := ‚ü®mem_iUnion‚ÇÇ.2 ‚ü®a, has, ha‚ü©, mem_iUnion‚ÇÇ.2 ‚ü®b, hbt, hb‚ü©‚ü©
    -- porting note: lift not implemented yet
    -- lift x to ordSeparatingSet s t using this
    suffices : ordConnectedComponent (ordSeparatingSet s t) x ‚äÜ [[a, b]]
    exact hsub (this <| ordConnectedProj_mem_ordConnectedComponent _ ‚ü®x, h'‚ü©) (mem_range_self _)
    rintro y (hy : [[x, y]] ‚äÜ ordSeparatingSet s t)
    rw [uIcc_of_le hab, mem_Icc, ‚Üê not_lt, ‚Üê not_lt]
    have sol1 := fun (hya : y < a) =>
        (disjoint_left (t := ordSeparatingSet s t)).1 disjoint_left_ordSeparatingSet has
          (hy <| Icc_subset_uIcc' ‚ü®hya.le, hax‚ü©)
    have sol2 := fun (hby : b < y) =>
        (disjoint_left (t := ordSeparatingSet s t)).1 disjoint_right_ordSeparatingSet hbt
          (hy <| Icc_subset_uIcc ‚ü®hxb, hby.le‚ü©)
    exact ‚ü®sol1, sol2‚ü©
#align set.disjoint_ord_t5_nhd Set.disjoint_ordT5Nhd
