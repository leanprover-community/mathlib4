/-
Copyright (c) 2017 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro
-/
import Mathlib.Data.Int.Order.Basic
import Mathlib.Data.Nat.Cast.Basic

#align_import data.int.cast.lemmas from "leanprover-community/mathlib"@"acebd8d49928f6ed8920e502a6c90674e75bd441"

/-!
# Cast of integers (additional theorems)

This file proves additional properties about the *canonical* homomorphism from
the integers into an additive group with a one (`Int.cast`),
particularly results involving algebraic homomorphisms or the order structure on `â„¤`
which were not available in the import dependencies of `Data.Int.Cast.Basic`.

## Main declarations

* `castAddHom`: `cast` bundled as an `AddMonoidHom`.
* `castRingHom`: `cast` bundled as a `RingHom`.
-/


open Nat

variable {F Î¹ Î± Î² : Type*}

namespace Int

/-- Coercion `â„• â†’ â„¤` as a `RingHom`. -/
def ofNatHom : â„• â†’+* â„¤ :=
  Nat.castRingHom â„¤
#align int.of_nat_hom Int.ofNatHom

-- Porting note: no need to be `@[simp]`, as `Nat.cast_pos` handles this.
-- @[simp]
theorem coe_nat_pos {n : â„•} : (0 : â„¤) < n â†” 0 < n :=
  Nat.cast_pos
#align int.coe_nat_pos Int.coe_nat_pos

theorem coe_nat_succ_pos (n : â„•) : 0 < (n.succ : â„¤) :=
  Int.coe_nat_pos.2 (succ_pos n)
#align int.coe_nat_succ_pos Int.coe_nat_succ_pos

lemma toNat_lt' {a : â„¤} {b : â„•} (hb : b â‰  0) : a.toNat < b â†” a < b := by
  rw [â†toNat_lt_toNat, toNat_coe_nat]; exact coe_nat_pos.2 hb.bot_lt
  -- âŠ¢ 0 < â†‘b
                                       -- ðŸŽ‰ no goals
#align int.to_nat_lt Int.toNat_lt'

lemma natMod_lt {a : â„¤} {b : â„•} (hb : b â‰  0) : a.natMod b < b :=
  (toNat_lt' hb).2 $ emod_lt_of_pos _ $ coe_nat_pos.2 hb.bot_lt
#align int.nat_mod_lt Int.natMod_lt

section cast

@[simp, norm_cast]
theorem cast_mul [NonAssocRing Î±] : âˆ€ m n, ((m * n : â„¤) : Î±) = m * n := fun m =>
  Int.inductionOn' m 0 (by simp) (fun k _ ih n => by simp [add_mul, ih]) fun k _ ih n => by
                           -- ðŸŽ‰ no goals
                                                     -- ðŸŽ‰ no goals
    simp [sub_mul, ih]
    -- ðŸŽ‰ no goals
#align int.cast_mul Int.cast_mulâ‚“ -- dubious translation, type involves HasLiftT

@[simp, norm_cast]
theorem cast_ite [AddGroupWithOne Î±] (P : Prop) [Decidable P] (m n : â„¤) :
    ((ite P m n : â„¤) : Î±) = ite P (m : Î±) (n : Î±) :=
  apply_ite _ _ _ _
#align int.cast_ite Int.cast_ite

/-- `coe : â„¤ â†’ Î±` as an `AddMonoidHom`. -/
def castAddHom (Î± : Type*) [AddGroupWithOne Î±] : â„¤ â†’+ Î± where
  toFun := Int.cast
  map_zero' := cast_zero
  map_add' := cast_add
#align int.cast_add_hom Int.castAddHom

@[simp]
theorem coe_castAddHom [AddGroupWithOne Î±] : â‡‘(castAddHom Î±) = fun x : â„¤ => (x : Î±) :=
  rfl
#align int.coe_cast_add_hom Int.coe_castAddHom

/-- `coe : â„¤ â†’ Î±` as a `RingHom`. -/
def castRingHom (Î± : Type*) [NonAssocRing Î±] : â„¤ â†’+* Î± where
  toFun := Int.cast
  map_zero' := cast_zero
  map_add' := cast_add
  map_one' := cast_one
  map_mul' := cast_mul
#align int.cast_ring_hom Int.castRingHom

@[simp]
theorem coe_castRingHom [NonAssocRing Î±] : â‡‘(castRingHom Î±) = fun x : â„¤ => (x : Î±) :=
  rfl
#align int.coe_cast_ring_hom Int.coe_castRingHom

theorem cast_commute [NonAssocRing Î±] : âˆ€ (m : â„¤) (x : Î±), Commute (â†‘m) x
  | (n : â„•), x => by simpa using n.cast_commute x
                     -- ðŸŽ‰ no goals
  | -[n+1], x => by
    simpa only [cast_negSucc, Commute.neg_left_iff, Commute.neg_right_iff] using
      (n + 1).cast_commute (-x)
#align int.cast_commute Int.cast_commute

theorem cast_comm [NonAssocRing Î±] (m : â„¤) (x : Î±) : (m : Î±) * x = x * m :=
  (cast_commute m x).eq
#align int.cast_comm Int.cast_comm

theorem commute_cast [NonAssocRing Î±] (x : Î±) (m : â„¤) : Commute x m :=
  (m.cast_commute x).symm
#align int.commute_cast Int.commute_cast

theorem cast_mono [OrderedRing Î±] : Monotone (fun x : â„¤ => (x : Î±)) := by
  intro m n h
  -- âŠ¢ (fun x => â†‘x) m â‰¤ (fun x => â†‘x) n
  rw [â† sub_nonneg] at h
  -- âŠ¢ (fun x => â†‘x) m â‰¤ (fun x => â†‘x) n
  lift n - m to â„• using h with k hk
  -- âŠ¢ â†‘m â‰¤ â†‘n
  rw [â† sub_nonneg, â† cast_sub, â† hk, cast_ofNat]
  -- âŠ¢ 0 â‰¤ â†‘k
  exact k.cast_nonneg
  -- ðŸŽ‰ no goals
#align int.cast_mono Int.cast_mono

@[simp]
theorem cast_nonneg [OrderedRing Î±] [Nontrivial Î±] : âˆ€ {n : â„¤}, (0 : Î±) â‰¤ n â†” 0 â‰¤ n
  | (n : â„•) => by simp
                  -- ðŸŽ‰ no goals
  | -[n+1] => by
    have : -(n : Î±) < 1 := lt_of_le_of_lt (by simp) zero_lt_one
    -- âŠ¢ 0 â‰¤ â†‘-[n+1] â†” 0 â‰¤ -[n+1]
    simpa [(negSucc_lt_zero n).not_le, â† sub_eq_add_neg, le_neg] using this.not_le
    -- ðŸŽ‰ no goals
#align int.cast_nonneg Int.cast_nonneg

@[simp, norm_cast]
theorem cast_le [OrderedRing Î±] [Nontrivial Î±] {m n : â„¤} : (m : Î±) â‰¤ n â†” m â‰¤ n := by
  rw [â† sub_nonneg, â† cast_sub, cast_nonneg, sub_nonneg]
  -- ðŸŽ‰ no goals
#align int.cast_le Int.cast_le

theorem cast_strictMono [OrderedRing Î±] [Nontrivial Î±] : StrictMono (fun x : â„¤ => (x : Î±)) :=
  strictMono_of_le_iff_le fun _ _ => cast_le.symm
#align int.cast_strict_mono Int.cast_strictMono

@[simp, norm_cast]
theorem cast_lt [OrderedRing Î±] [Nontrivial Î±] {m n : â„¤} : (m : Î±) < n â†” m < n :=
  cast_strictMono.lt_iff_lt
#align int.cast_lt Int.cast_lt

@[simp]
theorem cast_nonpos [OrderedRing Î±] [Nontrivial Î±] {n : â„¤} : (n : Î±) â‰¤ 0 â†” n â‰¤ 0 := by
  rw [â† cast_zero, cast_le]
  -- ðŸŽ‰ no goals
#align int.cast_nonpos Int.cast_nonpos

@[simp]
theorem cast_pos [OrderedRing Î±] [Nontrivial Î±] {n : â„¤} : (0 : Î±) < n â†” 0 < n := by
  rw [â† cast_zero, cast_lt]
  -- ðŸŽ‰ no goals
#align int.cast_pos Int.cast_pos

@[simp]
theorem cast_lt_zero [OrderedRing Î±] [Nontrivial Î±] {n : â„¤} : (n : Î±) < 0 â†” n < 0 := by
  rw [â† cast_zero, cast_lt]
  -- ðŸŽ‰ no goals
#align int.cast_lt_zero Int.cast_lt_zero

section LinearOrderedRing

variable [LinearOrderedRing Î±] {a b : â„¤} (n : â„¤)

@[simp, norm_cast]
theorem cast_min : (â†‘(min a b) : Î±) = min (a : Î±) (b : Î±) :=
  Monotone.map_min cast_mono
#align int.cast_min Int.cast_min

@[simp, norm_cast]
theorem cast_max : (â†‘(max a b) : Î±) = max (a : Î±) (b : Î±) :=
  Monotone.map_max cast_mono
#align int.cast_max Int.cast_max

@[simp, norm_cast]
theorem cast_abs : ((|a| : â„¤) : Î±) = |(a : Î±)| := by simp [abs_eq_max_neg]
                                                     -- ðŸŽ‰ no goals
#align int.cast_abs Int.cast_abs

theorem cast_one_le_of_pos (h : 0 < a) : (1 : Î±) â‰¤ a := by exact_mod_cast Int.add_one_le_of_lt h
                                                           -- ðŸŽ‰ no goals
#align int.cast_one_le_of_pos Int.cast_one_le_of_pos

theorem cast_le_neg_one_of_neg (h : a < 0) : (a : Î±) â‰¤ -1 := by
  rw [â† Int.cast_one, â† Int.cast_neg, cast_le]
  -- âŠ¢ a â‰¤ -1
  exact Int.le_sub_one_of_lt h
  -- ðŸŽ‰ no goals
#align int.cast_le_neg_one_of_neg Int.cast_le_neg_one_of_neg

variable (Î±) {n}

theorem cast_le_neg_one_or_one_le_cast_of_ne_zero (hn : n â‰  0) : (n : Î±) â‰¤ -1 âˆ¨ 1 â‰¤ (n : Î±) :=
  hn.lt_or_lt.imp cast_le_neg_one_of_neg cast_one_le_of_pos
#align int.cast_le_neg_one_or_one_le_cast_of_ne_zero Int.cast_le_neg_one_or_one_le_cast_of_ne_zero

variable {Î±} (n)

theorem nneg_mul_add_sq_of_abs_le_one {x : Î±} (hx : |x| â‰¤ 1) : (0 : Î±) â‰¤ n * x + n * n := by
  have hnx : 0 < n â†’ 0 â‰¤ x + n := fun hn => by
    have := _root_.add_le_add (neg_le_of_abs_le hx) (cast_one_le_of_pos hn)
    rwa [add_left_neg] at this
  have hnx' : n < 0 â†’ x + n â‰¤ 0 := fun hn => by
    have := _root_.add_le_add (le_of_abs_le hx) (cast_le_neg_one_of_neg hn)
    rwa [add_right_neg] at this
  rw [â† mul_add, mul_nonneg_iff]
  -- âŠ¢ 0 â‰¤ â†‘n âˆ§ 0 â‰¤ x + â†‘n âˆ¨ â†‘n â‰¤ 0 âˆ§ x + â†‘n â‰¤ 0
  rcases lt_trichotomy n 0 with (h | rfl | h)
  Â· exact Or.inr âŸ¨by exact_mod_cast h.le, hnx' hâŸ©
    -- ðŸŽ‰ no goals
  Â· simp [le_total 0 x]
    -- ðŸŽ‰ no goals
  Â· exact Or.inl âŸ¨by exact_mod_cast h.le, hnx hâŸ©
    -- ðŸŽ‰ no goals
#align int.nneg_mul_add_sq_of_abs_le_one Int.nneg_mul_add_sq_of_abs_le_one

theorem cast_natAbs : (n.natAbs : Î±) = |n| := by
  cases n
  -- âŠ¢ â†‘(natAbs (ofNat aâœ)) = â†‘|ofNat aâœ|
  Â· simp
    -- ðŸŽ‰ no goals
  Â· rw [abs_eq_natAbs, natAbs_negSucc, cast_succ, cast_ofNat, cast_succ]
    -- ðŸŽ‰ no goals
#align int.cast_nat_abs Int.cast_natAbs

end LinearOrderedRing

theorem coe_int_dvd [CommRing Î±] (m n : â„¤) (h : m âˆ£ n) : (m : Î±) âˆ£ (n : Î±) :=
  RingHom.map_dvd (Int.castRingHom Î±) h
#align int.coe_int_dvd Int.coe_int_dvd

end cast

end Int

open Int

namespace AddMonoidHom

variable {A : Type*}

/-- Two additive monoid homomorphisms `f`, `g` from `â„¤` to an additive monoid are equal
if `f 1 = g 1`. -/
@[ext high]
theorem ext_int [AddMonoid A] {f g : â„¤ â†’+ A} (h1 : f 1 = g 1) : f = g :=
  have : f.comp (Int.ofNatHom : â„• â†’+ â„¤) = g.comp (Int.ofNatHom : â„• â†’+ â„¤) := ext_nat' _ _ h1
  have this' : âˆ€ n : â„•, f n = g n := FunLike.ext_iff.1 this
  ext fun n => match n with
  | (n : â„•) => this' n
  | .negSucc n => eq_on_neg _ _ (this' <| n + 1)
#align add_monoid_hom.ext_int AddMonoidHom.ext_int

variable [AddGroupWithOne A]

theorem eq_int_castAddHom (f : â„¤ â†’+ A) (h1 : f 1 = 1) : f = Int.castAddHom A :=
  ext_int <| by simp [h1]
                -- ðŸŽ‰ no goals
#align add_monoid_hom.eq_int_cast_hom AddMonoidHom.eq_int_castAddHom

end AddMonoidHom

theorem eq_intCast' [AddGroupWithOne Î±] [AddMonoidHomClass F â„¤ Î±] (f : F) (hâ‚ : f 1 = 1) :
    âˆ€ n : â„¤, f n = n :=
  FunLike.ext_iff.1 <| (f : â„¤ â†’+ Î±).eq_int_castAddHom hâ‚
#align eq_int_cast' eq_intCast'

@[simp]
theorem Int.castAddHom_int : Int.castAddHom â„¤ = AddMonoidHom.id â„¤ :=
  ((AddMonoidHom.id â„¤).eq_int_castAddHom rfl).symm
#align int.cast_add_hom_int Int.castAddHom_int

namespace MonoidHom

variable {M : Type*} [Monoid M]

open Multiplicative

@[ext]
theorem ext_mint {f g : Multiplicative â„¤ â†’* M} (h1 : f (ofAdd 1) = g (ofAdd 1)) : f = g :=
  MonoidHom.toAdditive''.injective <| AddMonoidHom.ext_int <| Additive.toMul.injective h1
#align monoid_hom.ext_mint MonoidHom.ext_mint

/-- If two `MonoidHom`s agree on `-1` and the naturals then they are equal. -/
@[ext]
theorem ext_int {f g : â„¤ â†’* M} (h_neg_one : f (-1) = g (-1))
    (h_nat : f.comp Int.ofNatHom.toMonoidHom = g.comp Int.ofNatHom.toMonoidHom) : f = g := by
  ext (x | x)
  -- âŠ¢ â†‘f (ofNat x) = â†‘g (ofNat x)
  Â· exact (FunLike.congr_fun h_nat x : _)
    -- ðŸŽ‰ no goals
  Â· rw [Int.negSucc_eq, â† neg_one_mul, f.map_mul, g.map_mul]
    -- âŠ¢ â†‘f (-1) * â†‘f (â†‘x + 1) = â†‘g (-1) * â†‘g (â†‘x + 1)
    congr 1
    -- âŠ¢ â†‘f (â†‘x + 1) = â†‘g (â†‘x + 1)
    exact_mod_cast (FunLike.congr_fun h_nat (x + 1) : _)
    -- ðŸŽ‰ no goals
#align monoid_hom.ext_int MonoidHom.ext_int

end MonoidHom

namespace MonoidWithZeroHom

variable {M : Type*} [MonoidWithZero M]

/-- If two `MonoidWithZeroHom`s agree on `-1` and the naturals then they are equal. -/
@[ext]
theorem ext_int {f g : â„¤ â†’*â‚€ M} (h_neg_one : f (-1) = g (-1))
    (h_nat : f.comp Int.ofNatHom.toMonoidWithZeroHom = g.comp Int.ofNatHom.toMonoidWithZeroHom) :
    f = g :=
  toMonoidHom_injective <| MonoidHom.ext_int h_neg_one <|
    MonoidHom.ext (FunLike.congr_fun h_nat : _)
#align monoid_with_zero_hom.ext_int MonoidWithZeroHom.ext_int

end MonoidWithZeroHom

/-- If two `MonoidWithZeroHom`s agree on `-1` and the _positive_ naturals then they are equal. -/
theorem ext_int' [MonoidWithZero Î±] [MonoidWithZeroHomClass F â„¤ Î±] {f g : F}
    (h_neg_one : f (-1) = g (-1)) (h_pos : âˆ€ n : â„•, 0 < n â†’ f n = g n) : f = g :=
  (FunLike.ext _ _) fun n =>
    haveI :=
      FunLike.congr_fun
        (@MonoidWithZeroHom.ext_int _ _ (f : â„¤ â†’*â‚€ Î±) (g : â„¤ â†’*â‚€ Î±) h_neg_one <|
          MonoidWithZeroHom.ext_nat (h_pos _))
        n
    this
#align ext_int' ext_int'

section NonAssocRing

variable [NonAssocRing Î±] [NonAssocRing Î²]

@[simp]
theorem eq_intCast [RingHomClass F â„¤ Î±] (f : F) (n : â„¤) : f n = n :=
  eq_intCast' f (map_one _) n
#align eq_int_cast eq_intCast

@[simp]
theorem map_intCast [RingHomClass F Î± Î²] (f : F) (n : â„¤) : f n = n :=
  eq_intCast ((f : Î± â†’+* Î²).comp (Int.castRingHom Î±)) n
#align map_int_cast map_intCast

namespace RingHom

theorem eq_intCast' (f : â„¤ â†’+* Î±) : f = Int.castRingHom Î± :=
  RingHom.ext <| eq_intCast f
#align ring_hom.eq_int_cast' RingHom.eq_intCast'

theorem ext_int {R : Type*} [NonAssocSemiring R] (f g : â„¤ â†’+* R) : f = g :=
  coe_addMonoidHom_injective <| AddMonoidHom.ext_int <| f.map_one.trans g.map_one.symm
#align ring_hom.ext_int RingHom.ext_int

instance Int.subsingleton_ringHom {R : Type*} [NonAssocSemiring R] : Subsingleton (â„¤ â†’+* R) :=
  âŸ¨RingHom.ext_intâŸ©
#align ring_hom.int.subsingleton_ring_hom RingHom.Int.subsingleton_ringHom

end RingHom

end NonAssocRing

#align int.cast_id Int.cast_idâ‚“ -- dubious translation, type involves HasLiftT?

@[simp]
theorem Int.castRingHom_int : Int.castRingHom â„¤ = RingHom.id â„¤ :=
  (RingHom.id â„¤).eq_intCast'.symm
#align int.cast_ring_hom_int Int.castRingHom_int

namespace Pi

variable {Ï€ : Î¹ â†’ Type*} [âˆ€ i, IntCast (Ï€ i)]

instance intCast : IntCast (âˆ€ i, Ï€ i) :=
  { intCast := fun n _ â†¦ n }

theorem int_apply (n : â„¤) (i : Î¹) : (n : âˆ€ i, Ï€ i) i = n :=
  rfl
#align pi.int_apply Pi.int_apply

@[simp]
theorem coe_int (n : â„¤) : (n : âˆ€ i, Ï€ i) = fun _ => â†‘n :=
  rfl
#align pi.coe_int Pi.coe_int

end Pi

theorem Sum.elim_intCast_intCast {Î± Î² Î³ : Type*} [IntCast Î³] (n : â„¤) :
    Sum.elim (n : Î± â†’ Î³) (n : Î² â†’ Î³) = n :=
  @Sum.elim_lam_const_lam_const Î± Î² Î³ n
#align sum.elim_int_cast_int_cast Sum.elim_intCast_intCast

/-! ### Order dual -/


open OrderDual

instance [h : IntCast Î±] : IntCast Î±áµ’áµˆ :=
  h

instance [h : AddGroupWithOne Î±] : AddGroupWithOne Î±áµ’áµˆ :=
  h

instance [h : AddCommGroupWithOne Î±] : AddCommGroupWithOne Î±áµ’áµˆ :=
  h

@[simp]
theorem toDual_intCast [IntCast Î±] (n : â„¤) : toDual (n : Î±) = n :=
  rfl
#align to_dual_int_cast toDual_intCast

@[simp]
theorem ofDual_intCast [IntCast Î±] (n : â„¤) : (ofDual n : Î±) = n :=
  rfl
#align of_dual_int_cast ofDual_intCast

/-! ### Lexicographic order -/


instance [h : IntCast Î±] : IntCast (Lex Î±) :=
  h

instance [h : AddGroupWithOne Î±] : AddGroupWithOne (Lex Î±) :=
  h

instance [h : AddCommGroupWithOne Î±] : AddCommGroupWithOne (Lex Î±) :=
  h

@[simp]
theorem toLex_intCast [IntCast Î±] (n : â„¤) : toLex (n : Î±) = n :=
  rfl
#align to_lex_int_cast toLex_intCast

@[simp]
theorem ofLex_intCast [IntCast Î±] (n : â„¤) : (ofLex n : Î±) = n :=
  rfl
#align of_lex_int_cast ofLex_intCast
