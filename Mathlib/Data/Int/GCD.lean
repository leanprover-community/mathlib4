/-
Copyright (c) 2018 Guy Leroy. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sangwoo Jo (aka Jason), Guy Leroy, Johannes H√∂lzl, Mario Carneiro
-/
import Mathlib.Data.Nat.GCD.Basic
import Mathlib.Algebra.GroupPower.Lemmas
import Mathlib.Algebra.Ring.Regular
import Mathlib.Data.Int.Dvd.Basic
import Mathlib.Order.Bounds.Basic

#align_import data.int.gcd from "leanprover-community/mathlib"@"47a1a73351de8dd6c8d3d32b569c8e434b03ca47"

/-!
# Extended GCD and divisibility over ‚Ñ§

## Main definitions

* Given `x y : ‚Ñï`, `xgcd x y` computes the pair of integers `(a, b)` such that
  `gcd x y = x * a + y * b`. `gcdA x y` and `gcdB x y` are defined to be `a` and `b`,
  respectively.

## Main statements

* `gcd_eq_gcd_ab`: B√©zout's lemma, given `x y : ‚Ñï`, `gcd x y = x * gcdA x y + y * gcdB x y`.

## Tags

B√©zout's lemma, Bezout's lemma
-/

set_option autoImplicit true


/-! ### Extended Euclidean algorithm -/


namespace Nat

/-- Helper function for the extended GCD algorithm (`Nat.xgcd`). -/
def xgcdAux : ‚Ñï ‚Üí ‚Ñ§ ‚Üí ‚Ñ§ ‚Üí ‚Ñï ‚Üí ‚Ñ§ ‚Üí ‚Ñ§ ‚Üí ‚Ñï √ó ‚Ñ§ √ó ‚Ñ§
  | 0, _, _, r', s', t' => (r', s', t')
  | succ k, s, t, r', s', t' =>
    have : r' % succ k < succ k := mod_lt _ <| (succ_pos _).gt
    let q := r' / succ k
    xgcdAux (r' % succ k) (s' - q * s) (t' - q * t) (succ k) s t
#align nat.xgcd_aux Nat.xgcdAux

-- porting note: these are not in mathlib3; these equation lemmas are to fix
-- complaints by the Lean 4 `unusedHavesSuffices` linter obtained when `simp [xgcdAux]` is used.
theorem xgcdAux_zero : xgcdAux 0 s t r' s' t' = (r', s', t') := rfl

theorem xgcdAux_succ : xgcdAux (succ k) s t r' s' t' =
  xgcdAux (r' % succ k) (s' - (r' / succ k) * s) (t' - (r' / succ k) * t) (succ k) s t := rfl

@[simp]
theorem xgcd_zero_left {s t r' s' t'} : xgcdAux 0 s t r' s' t' = (r', s', t') := by simp [xgcdAux]
                                                                                    -- üéâ no goals
#align nat.xgcd_zero_left Nat.xgcd_zero_left

theorem xgcdAux_rec {r s t r' s' t'} (h : 0 < r) :
    xgcdAux r s t r' s' t' = xgcdAux (r' % r) (s' - r' / r * s) (t' - r' / r * t) r s t := by
  obtain ‚ü®r, rfl‚ü© := Nat.exists_eq_succ_of_ne_zero h.ne'
  -- ‚ä¢ xgcdAux (succ r) s t r' s' t' = xgcdAux (r' % succ r) (s' - ‚Üër' / ‚Üë(succ r)  ‚Ä¶
  rfl
  -- üéâ no goals
#align nat.xgcd_aux_rec Nat.xgcdAux_rec

/-- Use the extended GCD algorithm to generate the `a` and `b` values
  satisfying `gcd x y = x * a + y * b`. -/
def xgcd (x y : ‚Ñï) : ‚Ñ§ √ó ‚Ñ§ :=
  (xgcdAux x 1 0 y 0 1).2
#align nat.xgcd Nat.xgcd

/-- The extended GCD `a` value in the equation `gcd x y = x * a + y * b`. -/
def gcdA (x y : ‚Ñï) : ‚Ñ§ :=
  (xgcd x y).1
#align nat.gcd_a Nat.gcdA

/-- The extended GCD `b` value in the equation `gcd x y = x * a + y * b`. -/
def gcdB (x y : ‚Ñï) : ‚Ñ§ :=
  (xgcd x y).2
#align nat.gcd_b Nat.gcdB

@[simp]
theorem gcdA_zero_left {s : ‚Ñï} : gcdA 0 s = 0 := by
  unfold gcdA
  -- ‚ä¢ (xgcd 0 s).fst = 0
  rw [xgcd, xgcd_zero_left]
  -- üéâ no goals
#align nat.gcd_a_zero_left Nat.gcdA_zero_left

@[simp]
theorem gcdB_zero_left {s : ‚Ñï} : gcdB 0 s = 1 := by
  unfold gcdB
  -- ‚ä¢ (xgcd 0 s).snd = 1
  rw [xgcd, xgcd_zero_left]
  -- üéâ no goals
#align nat.gcd_b_zero_left Nat.gcdB_zero_left

@[simp]
theorem gcdA_zero_right {s : ‚Ñï} (h : s ‚â† 0) : gcdA s 0 = 1 := by
  unfold gcdA xgcd
  -- ‚ä¢ (xgcdAux s 1 0 0 0 1).snd.fst = 1
  obtain ‚ü®s, rfl‚ü© := Nat.exists_eq_succ_of_ne_zero h
  -- ‚ä¢ (xgcdAux (succ s) 1 0 0 0 1).snd.fst = 1
  -- Porting note (https://github.com/leanprover/lean4/issues/2330):
  -- `simp [xgcdAux_succ]` crashes Lean here
  rw [xgcdAux_succ]
  -- ‚ä¢ (xgcdAux (0 % succ s) (0 - ‚Üë0 / ‚Üë(succ s) * 1) (1 - ‚Üë0 / ‚Üë(succ s) * 0) (suc ‚Ä¶
  rfl
  -- üéâ no goals
#align nat.gcd_a_zero_right Nat.gcdA_zero_right

@[simp]
theorem gcdB_zero_right {s : ‚Ñï} (h : s ‚â† 0) : gcdB s 0 = 0 := by
  unfold gcdB xgcd
  -- ‚ä¢ (xgcdAux s 1 0 0 0 1).snd.snd = 0
  obtain ‚ü®s, rfl‚ü© := Nat.exists_eq_succ_of_ne_zero h
  -- ‚ä¢ (xgcdAux (succ s) 1 0 0 0 1).snd.snd = 0
  -- Porting note (https://github.com/leanprover/lean4/issues/2330):
  -- `simp [xgcdAux_succ]` crashes Lean here
  rw [xgcdAux_succ]
  -- ‚ä¢ (xgcdAux (0 % succ s) (0 - ‚Üë0 / ‚Üë(succ s) * 1) (1 - ‚Üë0 / ‚Üë(succ s) * 0) (suc ‚Ä¶
  rfl
  -- üéâ no goals
#align nat.gcd_b_zero_right Nat.gcdB_zero_right

@[simp]
theorem xgcdAux_fst (x y) : ‚àÄ s t s' t', (xgcdAux x s t y s' t').1 = gcd x y :=
  gcd.induction x y (by simp) fun x y h IH s t s' t' => by
                        -- üéâ no goals
    simp [xgcdAux_rec, h, IH]
    -- ‚ä¢ gcd (y % x) x = gcd x y
    rw [‚Üê gcd_rec]
    -- üéâ no goals
#align nat.xgcd_aux_fst Nat.xgcdAux_fst

theorem xgcdAux_val (x y) : xgcdAux x 1 0 y 0 1 = (gcd x y, xgcd x y) := by
  rw [xgcd, ‚Üê xgcdAux_fst x y 1 0 0 1]
  -- üéâ no goals
#align nat.xgcd_aux_val Nat.xgcdAux_val

theorem xgcd_val (x y) : xgcd x y = (gcdA x y, gcdB x y) := by
  unfold gcdA gcdB; cases xgcd x y; rfl
  -- ‚ä¢ xgcd x y = ((xgcd x y).fst, (xgcd x y).snd)
                    -- ‚ä¢ (fst‚úù, snd‚úù) = ((fst‚úù, snd‚úù).fst, (fst‚úù, snd‚úù).snd)
                                    -- üéâ no goals
#align nat.xgcd_val Nat.xgcd_val

section

variable (x y : ‚Ñï)

private def P : ‚Ñï √ó ‚Ñ§ √ó ‚Ñ§ ‚Üí Prop
  | (r, s, t) => (r : ‚Ñ§) = x * s + y * t

theorem xgcdAux_P {r r'} :
    ‚àÄ {s t s' t'}, P x y (r, s, t) ‚Üí P x y (r', s', t') ‚Üí P x y (xgcdAux r s t r' s' t') := by
  induction r, r' using gcd.induction with
  | H0 => simp
  | H1 a b h IH =>
    intro s t s' t' p p'
    rw [xgcdAux_rec h]; refine' IH _ p; dsimp [P] at *
    rw [Int.emod_def]; generalize (b / a : ‚Ñ§) = k
    rw [p, p', mul_sub, sub_add_eq_add_sub, mul_sub, add_mul, mul_comm k t, mul_comm k s,
      ‚Üê mul_assoc, ‚Üê mul_assoc, add_comm (x * s * k), ‚Üê add_sub_assoc, sub_sub]
set_option linter.uppercaseLean3 false in
#align nat.xgcd_aux_P Nat.xgcdAux_P

/-- **B√©zout's lemma**: given `x y : ‚Ñï`, `gcd x y = x * a + y * b`, where `a = gcd_a x y` and
`b = gcd_b x y` are computed by the extended Euclidean algorithm.
-/
theorem gcd_eq_gcd_ab : (gcd x y : ‚Ñ§) = x * gcdA x y + y * gcdB x y := by
  have := @xgcdAux_P x y x y 1 0 0 1 (by simp [P]) (by simp [P])
  -- ‚ä¢ ‚Üë(gcd x y) = ‚Üëx * gcdA x y + ‚Üëy * gcdB x y
  rwa [xgcdAux_val, xgcd_val] at this
  -- üéâ no goals
#align nat.gcd_eq_gcd_ab Nat.gcd_eq_gcd_ab

end

theorem exists_mul_emod_eq_gcd {k n : ‚Ñï} (hk : gcd n k < k) : ‚àÉ m, n * m % k = gcd n k := by
  have hk' := Int.ofNat_ne_zero.2 (ne_of_gt (lt_of_le_of_lt (zero_le (gcd n k)) hk))
  -- ‚ä¢ ‚àÉ m, n * m % k = gcd n k
  have key := congr_arg (fun (m : ‚Ñ§) => (m % k).toNat) (gcd_eq_gcd_ab n k)
  -- ‚ä¢ ‚àÉ m, n * m % k = gcd n k
  simp only at key
  -- ‚ä¢ ‚àÉ m, n * m % k = gcd n k
  rw [Int.add_mul_emod_self_left, ‚Üê Int.coe_nat_mod, Int.toNat_coe_nat, mod_eq_of_lt hk] at key
  -- ‚ä¢ ‚àÉ m, n * m % k = gcd n k
  refine' ‚ü®(n.gcdA k % k).toNat, Eq.trans (Int.ofNat.inj _) key.symm‚ü©
  -- ‚ä¢ Int.ofNat (n * Int.toNat (gcdA n k % ‚Üëk) % k) = Int.ofNat (Int.toNat (‚Üën * g ‚Ä¶
  rw [Int.ofNat_eq_coe, Int.coe_nat_mod, Int.ofNat_mul, Int.toNat_of_nonneg (Int.emod_nonneg _ hk'),
    Int.ofNat_eq_coe, Int.toNat_of_nonneg (Int.emod_nonneg _ hk'), Int.mul_emod, Int.emod_emod,
    ‚Üê Int.mul_emod]
#align nat.exists_mul_mod_eq_gcd Nat.exists_mul_emod_eq_gcd

theorem exists_mul_emod_eq_one_of_coprime {k n : ‚Ñï} (hkn : coprime n k) (hk : 1 < k) :
    ‚àÉ m, n * m % k = 1 :=
  Exists.recOn (exists_mul_emod_eq_gcd (lt_of_le_of_lt (le_of_eq hkn) hk)) fun m hm ‚Ü¶
    ‚ü®m, hm.trans hkn‚ü©
#align nat.exists_mul_mod_eq_one_of_coprime Nat.exists_mul_emod_eq_one_of_coprime

end Nat

/-! ### Divisibility over ‚Ñ§ -/


namespace Int

theorem gcd_def (i j : ‚Ñ§) : gcd i j = Nat.gcd i.natAbs j.natAbs := rfl

protected theorem coe_nat_gcd (m n : ‚Ñï) : Int.gcd ‚Üëm ‚Üën = Nat.gcd m n :=
  rfl
#align int.coe_nat_gcd Int.coe_nat_gcd

/-- The extended GCD `a` value in the equation `gcd x y = x * a + y * b`. -/
def gcdA : ‚Ñ§ ‚Üí ‚Ñ§ ‚Üí ‚Ñ§
  | ofNat m, n => m.gcdA n.natAbs
  | -[m+1], n => -m.succ.gcdA n.natAbs
#align int.gcd_a Int.gcdA

/-- The extended GCD `b` value in the equation `gcd x y = x * a + y * b`. -/
def gcdB : ‚Ñ§ ‚Üí ‚Ñ§ ‚Üí ‚Ñ§
  | m, ofNat n => m.natAbs.gcdB n
  | m, -[n+1] => -m.natAbs.gcdB n.succ
#align int.gcd_b Int.gcdB

/-- **B√©zout's lemma** -/
theorem gcd_eq_gcd_ab : ‚àÄ x y : ‚Ñ§, (gcd x y : ‚Ñ§) = x * gcdA x y + y * gcdB x y
  | (m : ‚Ñï), (n : ‚Ñï) => Nat.gcd_eq_gcd_ab _ _
  | (m : ‚Ñï), -[n+1] =>
    show (_ : ‚Ñ§) = _ + -(n + 1) * -_ by rw [neg_mul_neg]; apply Nat.gcd_eq_gcd_ab
                                        -- ‚ä¢ ‚Üë(gcd ‚Üëm -[n+1]) = ‚Üëm * gcdA ‚Üëm -[n+1] + (‚Üën + 1) * Nat.gcdB (natAbs ‚Üëm) (Na ‚Ä¶
                                                          -- üéâ no goals
  | -[m+1], (n : ‚Ñï) =>
    show (_ : ‚Ñ§) = -(m + 1) * -_ + _ by rw [neg_mul_neg]; apply Nat.gcd_eq_gcd_ab
                                        -- ‚ä¢ ‚Üë(gcd -[m+1] ‚Üën) = (‚Üëm + 1) * Nat.gcdA (Nat.succ m) (natAbs ‚Üën) + ‚Üën * gcdB  ‚Ä¶
                                                          -- üéâ no goals
  | -[m+1], -[n+1] =>
    show (_ : ‚Ñ§) = -(m + 1) * -_ + -(n + 1) * -_ by
      rw [neg_mul_neg, neg_mul_neg]
      -- ‚ä¢ ‚Üë(gcd -[m+1] -[n+1]) = (‚Üëm + 1) * Nat.gcdA (Nat.succ m) (natAbs -[n+1]) + (‚Üë ‚Ä¶
      apply Nat.gcd_eq_gcd_ab
      -- üéâ no goals
#align int.gcd_eq_gcd_ab Int.gcd_eq_gcd_ab

theorem natAbs_ediv (a b : ‚Ñ§) (H : b ‚à£ a) : natAbs (a / b) = natAbs a / natAbs b := by
  rcases Nat.eq_zero_or_pos (natAbs b) with (h | h)
  -- ‚ä¢ natAbs (a / b) = natAbs a / natAbs b
  ¬∑ rw [natAbs_eq_zero.1 h]
    -- ‚ä¢ natAbs (a / 0) = natAbs a / natAbs 0
    simp [Int.ediv_zero]
    -- üéâ no goals
  calc
    natAbs (a / b) = natAbs (a / b) * 1 := by rw [mul_one]
    _ = natAbs (a / b) * (natAbs b / natAbs b) := by rw [Nat.div_self h]
    _ = natAbs (a / b) * natAbs b / natAbs b := by rw [Nat.mul_div_assoc _ dvd_rfl]
    _ = natAbs (a / b * b) / natAbs b := by rw [natAbs_mul (a / b) b]
    _ = natAbs a / natAbs b := by rw [Int.ediv_mul_cancel H]
#align int.nat_abs_div Int.natAbs_ediv

theorem dvd_of_mul_dvd_mul_left {i j k : ‚Ñ§} (k_non_zero : k ‚â† 0) (H : k * i ‚à£ k * j) : i ‚à£ j :=
  Dvd.elim H fun l H1 => by rw [mul_assoc] at H1; exact ‚ü®_, mul_left_cancel‚ÇÄ k_non_zero H1‚ü©
                            -- ‚ä¢ i ‚à£ j
                                                  -- üéâ no goals
#align int.dvd_of_mul_dvd_mul_left Int.dvd_of_mul_dvd_mul_left

theorem dvd_of_mul_dvd_mul_right {i j k : ‚Ñ§} (k_non_zero : k ‚â† 0) (H : i * k ‚à£ j * k) : i ‚à£ j := by
  rw [mul_comm i k, mul_comm j k] at H; exact dvd_of_mul_dvd_mul_left k_non_zero H
  -- ‚ä¢ i ‚à£ j
                                        -- üéâ no goals
#align int.dvd_of_mul_dvd_mul_right Int.dvd_of_mul_dvd_mul_right

/-- ‚Ñ§ specific version of least common multiple. -/
def lcm (i j : ‚Ñ§) : ‚Ñï :=
  Nat.lcm (natAbs i) (natAbs j)
#align int.lcm Int.lcm

theorem lcm_def (i j : ‚Ñ§) : lcm i j = Nat.lcm (natAbs i) (natAbs j) :=
  rfl
#align int.lcm_def Int.lcm_def

protected theorem coe_nat_lcm (m n : ‚Ñï) : Int.lcm ‚Üëm ‚Üën = Nat.lcm m n :=
  rfl
#align int.coe_nat_lcm Int.coe_nat_lcm

theorem gcd_dvd_left (i j : ‚Ñ§) : (gcd i j : ‚Ñ§) ‚à£ i :=
  dvd_natAbs.mp <| coe_nat_dvd.mpr <| Nat.gcd_dvd_left _ _
#align int.gcd_dvd_left Int.gcd_dvd_left

theorem gcd_dvd_right (i j : ‚Ñ§) : (gcd i j : ‚Ñ§) ‚à£ j :=
  dvd_natAbs.mp <| coe_nat_dvd.mpr <| Nat.gcd_dvd_right _ _
#align int.gcd_dvd_right Int.gcd_dvd_right

theorem dvd_gcd {i j k : ‚Ñ§} (h1 : k ‚à£ i) (h2 : k ‚à£ j) : k ‚à£ gcd i j :=
  natAbs_dvd.1 <|
    coe_nat_dvd.2 <| Nat.dvd_gcd (natAbs_dvd_natAbs.2 h1) (natAbs_dvd_natAbs.2 h2)
#align int.dvd_gcd Int.dvd_gcd

theorem gcd_mul_lcm (i j : ‚Ñ§) : gcd i j * lcm i j = natAbs (i * j) := by
  rw [Int.gcd, Int.lcm, Nat.gcd_mul_lcm, natAbs_mul]
  -- üéâ no goals
#align int.gcd_mul_lcm Int.gcd_mul_lcm

theorem gcd_comm (i j : ‚Ñ§) : gcd i j = gcd j i :=
  Nat.gcd_comm _ _
#align int.gcd_comm Int.gcd_comm

theorem gcd_assoc (i j k : ‚Ñ§) : gcd (gcd i j) k = gcd i (gcd j k) :=
  Nat.gcd_assoc _ _ _
#align int.gcd_assoc Int.gcd_assoc

@[simp]
theorem gcd_self (i : ‚Ñ§) : gcd i i = natAbs i := by simp [gcd]
                                                    -- üéâ no goals
#align int.gcd_self Int.gcd_self

@[simp]
theorem gcd_zero_left (i : ‚Ñ§) : gcd 0 i = natAbs i := by simp [gcd]
                                                         -- üéâ no goals
#align int.gcd_zero_left Int.gcd_zero_left

@[simp]
theorem gcd_zero_right (i : ‚Ñ§) : gcd i 0 = natAbs i := by simp [gcd]
                                                          -- üéâ no goals
#align int.gcd_zero_right Int.gcd_zero_right

@[simp]
theorem gcd_one_left (i : ‚Ñ§) : gcd 1 i = 1 :=
  Nat.gcd_one_left _
#align int.gcd_one_left Int.gcd_one_left

@[simp]
theorem gcd_one_right (i : ‚Ñ§) : gcd i 1 = 1 :=
  Nat.gcd_one_right _
#align int.gcd_one_right Int.gcd_one_right

@[simp]
theorem gcd_neg_right {x y : ‚Ñ§} : gcd x (-y) = gcd x y := by rw [Int.gcd, Int.gcd, natAbs_neg]
                                                             -- üéâ no goals
#align int.gcd_neg_right Int.gcd_neg_right

@[simp]
theorem gcd_neg_left {x y : ‚Ñ§} : gcd (-x) y = gcd x y := by rw [Int.gcd, Int.gcd, natAbs_neg]
                                                            -- üéâ no goals
#align int.gcd_neg_left Int.gcd_neg_left

theorem gcd_mul_left (i j k : ‚Ñ§) : gcd (i * j) (i * k) = natAbs i * gcd j k := by
  rw [Int.gcd, Int.gcd, natAbs_mul, natAbs_mul]
  -- ‚ä¢ Nat.gcd (natAbs i * natAbs j) (natAbs i * natAbs k) = natAbs i * Nat.gcd (na ‚Ä¶
  apply Nat.gcd_mul_left
  -- üéâ no goals
#align int.gcd_mul_left Int.gcd_mul_left

theorem gcd_mul_right (i j k : ‚Ñ§) : gcd (i * j) (k * j) = gcd i k * natAbs j := by
  rw [Int.gcd, Int.gcd, natAbs_mul, natAbs_mul]
  -- ‚ä¢ Nat.gcd (natAbs i * natAbs j) (natAbs k * natAbs j) = Nat.gcd (natAbs i) (na ‚Ä¶
  apply Nat.gcd_mul_right
  -- üéâ no goals
#align int.gcd_mul_right Int.gcd_mul_right

theorem gcd_pos_of_ne_zero_left {i : ‚Ñ§} (j : ‚Ñ§) (hi : i ‚â† 0) : 0 < gcd i j :=
  Nat.gcd_pos_of_pos_left _ $ natAbs_pos.2 hi
#align int.gcd_pos_of_ne_zero_left Int.gcd_pos_of_ne_zero_left

theorem gcd_pos_of_ne_zero_right (i : ‚Ñ§) {j : ‚Ñ§} (hj : j ‚â† 0) : 0 < gcd i j :=
  Nat.gcd_pos_of_pos_right _ $ natAbs_pos.2 hj
#align int.gcd_pos_of_ne_zero_right Int.gcd_pos_of_ne_zero_right

theorem gcd_eq_zero_iff {i j : ‚Ñ§} : gcd i j = 0 ‚Üî i = 0 ‚àß j = 0 := by
  rw [gcd, Nat.gcd_eq_zero_iff, natAbs_eq_zero, natAbs_eq_zero]
  -- üéâ no goals
#align int.gcd_eq_zero_iff Int.gcd_eq_zero_iff

theorem gcd_pos_iff {i j : ‚Ñ§} : 0 < gcd i j ‚Üî i ‚â† 0 ‚à® j ‚â† 0 :=
  pos_iff_ne_zero.trans <| gcd_eq_zero_iff.not.trans not_and_or
#align int.gcd_pos_iff Int.gcd_pos_iff

theorem gcd_div {i j k : ‚Ñ§} (H1 : k ‚à£ i) (H2 : k ‚à£ j) :
    gcd (i / k) (j / k) = gcd i j / natAbs k := by
  rw [gcd, natAbs_ediv i k H1, natAbs_ediv j k H2]
  -- ‚ä¢ Nat.gcd (natAbs i / natAbs k) (natAbs j / natAbs k) = gcd i j / natAbs k
  exact Nat.gcd_div (natAbs_dvd_natAbs.mpr H1) (natAbs_dvd_natAbs.mpr H2)
  -- üéâ no goals
#align int.gcd_div Int.gcd_div

theorem gcd_div_gcd_div_gcd {i j : ‚Ñ§} (H : 0 < gcd i j) : gcd (i / gcd i j) (j / gcd i j) = 1 := by
  rw [gcd_div (gcd_dvd_left i j) (gcd_dvd_right i j), natAbs_ofNat, Nat.div_self H]
  -- üéâ no goals
#align int.gcd_div_gcd_div_gcd Int.gcd_div_gcd_div_gcd

theorem gcd_dvd_gcd_of_dvd_left {i k : ‚Ñ§} (j : ‚Ñ§) (H : i ‚à£ k) : gcd i j ‚à£ gcd k j :=
  Int.coe_nat_dvd.1 <| dvd_gcd ((gcd_dvd_left i j).trans H) (gcd_dvd_right i j)
#align int.gcd_dvd_gcd_of_dvd_left Int.gcd_dvd_gcd_of_dvd_left

theorem gcd_dvd_gcd_of_dvd_right {i k : ‚Ñ§} (j : ‚Ñ§) (H : i ‚à£ k) : gcd j i ‚à£ gcd j k :=
  Int.coe_nat_dvd.1 <| dvd_gcd (gcd_dvd_left j i) ((gcd_dvd_right j i).trans H)
#align int.gcd_dvd_gcd_of_dvd_right Int.gcd_dvd_gcd_of_dvd_right

theorem gcd_dvd_gcd_mul_left (i j k : ‚Ñ§) : gcd i j ‚à£ gcd (k * i) j :=
  gcd_dvd_gcd_of_dvd_left _ (dvd_mul_left _ _)
#align int.gcd_dvd_gcd_mul_left Int.gcd_dvd_gcd_mul_left

theorem gcd_dvd_gcd_mul_right (i j k : ‚Ñ§) : gcd i j ‚à£ gcd (i * k) j :=
  gcd_dvd_gcd_of_dvd_left _ (dvd_mul_right _ _)
#align int.gcd_dvd_gcd_mul_right Int.gcd_dvd_gcd_mul_right

theorem gcd_dvd_gcd_mul_left_right (i j k : ‚Ñ§) : gcd i j ‚à£ gcd i (k * j) :=
  gcd_dvd_gcd_of_dvd_right _ (dvd_mul_left _ _)
#align int.gcd_dvd_gcd_mul_left_right Int.gcd_dvd_gcd_mul_left_right

theorem gcd_dvd_gcd_mul_right_right (i j k : ‚Ñ§) : gcd i j ‚à£ gcd i (j * k) :=
  gcd_dvd_gcd_of_dvd_right _ (dvd_mul_right _ _)
#align int.gcd_dvd_gcd_mul_right_right Int.gcd_dvd_gcd_mul_right_right

theorem gcd_eq_left {i j : ‚Ñ§} (H : i ‚à£ j) : gcd i j = natAbs i :=
  Nat.dvd_antisymm (Nat.gcd_dvd_left _ _) (Nat.dvd_gcd dvd_rfl (natAbs_dvd_natAbs.mpr H))
#align int.gcd_eq_left Int.gcd_eq_left

theorem gcd_eq_right {i j : ‚Ñ§} (H : j ‚à£ i) : gcd i j = natAbs j := by rw [gcd_comm, gcd_eq_left H]
                                                                      -- üéâ no goals
#align int.gcd_eq_right Int.gcd_eq_right

theorem ne_zero_of_gcd {x y : ‚Ñ§} (hc : gcd x y ‚â† 0) : x ‚â† 0 ‚à® y ‚â† 0 := by
  contrapose! hc
  -- ‚ä¢ gcd x y = 0
  rw [hc.left, hc.right, gcd_zero_right, natAbs_zero]
  -- üéâ no goals
#align int.ne_zero_of_gcd Int.ne_zero_of_gcd

theorem exists_gcd_one {m n : ‚Ñ§} (H : 0 < gcd m n) :
    ‚àÉ m' n' : ‚Ñ§, gcd m' n' = 1 ‚àß m = m' * gcd m n ‚àß n = n' * gcd m n :=
  ‚ü®_, _, gcd_div_gcd_div_gcd H, (Int.ediv_mul_cancel (gcd_dvd_left m n)).symm,
    (Int.ediv_mul_cancel (gcd_dvd_right m n)).symm‚ü©
#align int.exists_gcd_one Int.exists_gcd_one

theorem exists_gcd_one' {m n : ‚Ñ§} (H : 0 < gcd m n) :
    ‚àÉ (g : ‚Ñï) (m' n' : ‚Ñ§), 0 < g ‚àß gcd m' n' = 1 ‚àß m = m' * g ‚àß n = n' * g :=
  let ‚ü®m', n', h‚ü© := exists_gcd_one H
  ‚ü®_, m', n', H, h‚ü©
#align int.exists_gcd_one' Int.exists_gcd_one'

theorem pow_dvd_pow_iff {m n : ‚Ñ§} {k : ‚Ñï} (k0 : 0 < k) : m ^ k ‚à£ n ^ k ‚Üî m ‚à£ n := by
  refine' ‚ü®fun h => _, fun h => pow_dvd_pow_of_dvd h _‚ü©
  -- ‚ä¢ m ‚à£ n
  rwa [‚Üê natAbs_dvd_natAbs, ‚Üê Nat.pow_dvd_pow_iff k0, ‚Üê Int.natAbs_pow, ‚Üê Int.natAbs_pow,
    natAbs_dvd_natAbs]
#align int.pow_dvd_pow_iff Int.pow_dvd_pow_iff

theorem gcd_dvd_iff {a b : ‚Ñ§} {n : ‚Ñï} : gcd a b ‚à£ n ‚Üî ‚àÉ x y : ‚Ñ§, ‚Üën = a * x + b * y := by
  constructor
  -- ‚ä¢ gcd a b ‚à£ n ‚Üí ‚àÉ x y, ‚Üën = a * x + b * y
  ¬∑ intro h
    -- ‚ä¢ ‚àÉ x y, ‚Üën = a * x + b * y
    rw [‚Üê Nat.mul_div_cancel' h, Int.ofNat_mul, gcd_eq_gcd_ab, add_mul, mul_assoc, mul_assoc]
    -- ‚ä¢ ‚àÉ x y, a * (gcdA a b * ‚Üë(n / gcd a b)) + b * (gcdB a b * ‚Üë(n / gcd a b)) = a ‚Ä¶
    exact ‚ü®_, _, rfl‚ü©
    -- üéâ no goals
  ¬∑ rintro ‚ü®x, y, h‚ü©
    -- ‚ä¢ gcd a b ‚à£ n
    rw [‚Üê Int.coe_nat_dvd, h]
    -- ‚ä¢ ‚Üë(gcd a b) ‚à£ a * x + b * y
    exact
      dvd_add (dvd_mul_of_dvd_left (gcd_dvd_left a b) _) (dvd_mul_of_dvd_left (gcd_dvd_right a b) y)
#align int.gcd_dvd_iff Int.gcd_dvd_iff

theorem gcd_greatest {a b d : ‚Ñ§} (hd_pos : 0 ‚â§ d) (hda : d ‚à£ a) (hdb : d ‚à£ b)
    (hd : ‚àÄ e : ‚Ñ§, e ‚à£ a ‚Üí e ‚à£ b ‚Üí e ‚à£ d) : d = gcd a b :=
  dvd_antisymm hd_pos (ofNat_zero_le (gcd a b)) (dvd_gcd hda hdb)
    (hd _ (gcd_dvd_left a b) (gcd_dvd_right a b))
#align int.gcd_greatest Int.gcd_greatest

/-- Euclid's lemma: if `a ‚à£ b * c` and `gcd a c = 1` then `a ‚à£ b`.
Compare with `IsCoprime.dvd_of_dvd_mul_left` and
`UniqueFactorizationMonoid.dvd_of_dvd_mul_left_of_no_prime_factors` -/
theorem dvd_of_dvd_mul_left_of_gcd_one {a b c : ‚Ñ§} (habc : a ‚à£ b * c) (hab : gcd a c = 1) :
    a ‚à£ b := by
  have := gcd_eq_gcd_ab a c
  -- ‚ä¢ a ‚à£ b
  simp only [hab, Int.ofNat_zero, Int.ofNat_succ, zero_add] at this
  -- ‚ä¢ a ‚à£ b
  have : b * a * gcdA a c + b * c * gcdB a c = b := by simp [mul_assoc, ‚Üê mul_add, ‚Üê this]
  -- ‚ä¢ a ‚à£ b
  rw [‚Üê this]
  -- ‚ä¢ a ‚à£ b * a * gcdA a c + b * c * gcdB a c
  exact dvd_add (dvd_mul_of_dvd_left (dvd_mul_left a b) _) (dvd_mul_of_dvd_left habc _)
  -- üéâ no goals
#align int.dvd_of_dvd_mul_left_of_gcd_one Int.dvd_of_dvd_mul_left_of_gcd_one

/-- Euclid's lemma: if `a ‚à£ b * c` and `gcd a b = 1` then `a ‚à£ c`.
Compare with `IsCoprime.dvd_of_dvd_mul_right` and
`UniqueFactorizationMonoid.dvd_of_dvd_mul_right_of_no_prime_factors` -/
theorem dvd_of_dvd_mul_right_of_gcd_one {a b c : ‚Ñ§} (habc : a ‚à£ b * c) (hab : gcd a b = 1) :
    a ‚à£ c := by
  rw [mul_comm] at habc
  -- ‚ä¢ a ‚à£ c
  exact dvd_of_dvd_mul_left_of_gcd_one habc hab
  -- üéâ no goals
#align int.dvd_of_dvd_mul_right_of_gcd_one Int.dvd_of_dvd_mul_right_of_gcd_one

/-- For nonzero integers `a` and `b`, `gcd a b` is the smallest positive natural number that can be
written in the form `a * x + b * y` for some pair of integers `x` and `y` -/
theorem gcd_least_linear {a b : ‚Ñ§} (ha : a ‚â† 0) :
    IsLeast { n : ‚Ñï | 0 < n ‚àß ‚àÉ x y : ‚Ñ§, ‚Üën = a * x + b * y } (a.gcd b) := by
  simp_rw [‚Üê gcd_dvd_iff]
  -- ‚ä¢ IsLeast {n | 0 < n ‚àß gcd a b ‚à£ n} (gcd a b)
  constructor
  -- ‚ä¢ gcd a b ‚àà {n | 0 < n ‚àß gcd a b ‚à£ n}
  ¬∑ simpa [and_true_iff, dvd_refl, Set.mem_setOf_eq] using gcd_pos_of_ne_zero_left b ha
    -- üéâ no goals
  ¬∑ simp only [lowerBounds, and_imp, Set.mem_setOf_eq]
    -- ‚ä¢ ‚àÄ ‚¶Éa_1 : ‚Ñï‚¶Ñ, 0 < a_1 ‚Üí gcd a b ‚à£ a_1 ‚Üí gcd a b ‚â§ a_1
    exact fun n hn_pos hn => Nat.le_of_dvd hn_pos hn
    -- üéâ no goals
#align int.gcd_least_linear Int.gcd_least_linear

/-! ### lcm -/


theorem lcm_comm (i j : ‚Ñ§) : lcm i j = lcm j i := by
  rw [Int.lcm, Int.lcm]
  -- ‚ä¢ Nat.lcm (natAbs i) (natAbs j) = Nat.lcm (natAbs j) (natAbs i)
  exact Nat.lcm_comm _ _
  -- üéâ no goals
#align int.lcm_comm Int.lcm_comm

theorem lcm_assoc (i j k : ‚Ñ§) : lcm (lcm i j) k = lcm i (lcm j k) := by
  rw [Int.lcm, Int.lcm, Int.lcm, Int.lcm, natAbs_ofNat, natAbs_ofNat]
  -- ‚ä¢ Nat.lcm (Nat.lcm (natAbs i) (natAbs j)) (natAbs k) = Nat.lcm (natAbs i) (Nat ‚Ä¶
  apply Nat.lcm_assoc
  -- üéâ no goals
#align int.lcm_assoc Int.lcm_assoc

@[simp]
theorem lcm_zero_left (i : ‚Ñ§) : lcm 0 i = 0 := by
  rw [Int.lcm]
  -- ‚ä¢ Nat.lcm (natAbs 0) (natAbs i) = 0
  apply Nat.lcm_zero_left
  -- üéâ no goals
#align int.lcm_zero_left Int.lcm_zero_left

@[simp]
theorem lcm_zero_right (i : ‚Ñ§) : lcm i 0 = 0 := by
  rw [Int.lcm]
  -- ‚ä¢ Nat.lcm (natAbs i) (natAbs 0) = 0
  apply Nat.lcm_zero_right
  -- üéâ no goals
#align int.lcm_zero_right Int.lcm_zero_right

@[simp]
theorem lcm_one_left (i : ‚Ñ§) : lcm 1 i = natAbs i := by
  rw [Int.lcm]
  -- ‚ä¢ Nat.lcm (natAbs 1) (natAbs i) = natAbs i
  apply Nat.lcm_one_left
  -- üéâ no goals
#align int.lcm_one_left Int.lcm_one_left

@[simp]
theorem lcm_one_right (i : ‚Ñ§) : lcm i 1 = natAbs i := by
  rw [Int.lcm]
  -- ‚ä¢ Nat.lcm (natAbs i) (natAbs 1) = natAbs i
  apply Nat.lcm_one_right
  -- üéâ no goals
#align int.lcm_one_right Int.lcm_one_right

@[simp]
theorem lcm_self (i : ‚Ñ§) : lcm i i = natAbs i := by
  rw [Int.lcm]
  -- ‚ä¢ Nat.lcm (natAbs i) (natAbs i) = natAbs i
  apply Nat.lcm_self
  -- üéâ no goals
#align int.lcm_self Int.lcm_self

theorem dvd_lcm_left (i j : ‚Ñ§) : i ‚à£ lcm i j := by
  rw [Int.lcm]
  -- ‚ä¢ i ‚à£ ‚Üë(Nat.lcm (natAbs i) (natAbs j))
  apply coe_nat_dvd_right.mpr
  -- ‚ä¢ natAbs i ‚à£ Nat.lcm (natAbs i) (natAbs j)
  apply Nat.dvd_lcm_left
  -- üéâ no goals
#align int.dvd_lcm_left Int.dvd_lcm_left

theorem dvd_lcm_right (i j : ‚Ñ§) : j ‚à£ lcm i j := by
  rw [Int.lcm]
  -- ‚ä¢ j ‚à£ ‚Üë(Nat.lcm (natAbs i) (natAbs j))
  apply coe_nat_dvd_right.mpr
  -- ‚ä¢ natAbs j ‚à£ Nat.lcm (natAbs i) (natAbs j)
  apply Nat.dvd_lcm_right
  -- üéâ no goals
#align int.dvd_lcm_right Int.dvd_lcm_right

theorem lcm_dvd {i j k : ‚Ñ§} : i ‚à£ k ‚Üí j ‚à£ k ‚Üí (lcm i j : ‚Ñ§) ‚à£ k := by
  rw [Int.lcm]
  -- ‚ä¢ i ‚à£ k ‚Üí j ‚à£ k ‚Üí ‚Üë(Nat.lcm (natAbs i) (natAbs j)) ‚à£ k
  intro hi hj
  -- ‚ä¢ ‚Üë(Nat.lcm (natAbs i) (natAbs j)) ‚à£ k
  exact coe_nat_dvd_left.mpr (Nat.lcm_dvd (natAbs_dvd_natAbs.mpr hi) (natAbs_dvd_natAbs.mpr hj))
  -- üéâ no goals
#align int.lcm_dvd Int.lcm_dvd

end Int

@[to_additive gcd_nsmul_eq_zero]
theorem pow_gcd_eq_one {M : Type*} [Monoid M] (x : M) {m n : ‚Ñï} (hm : x ^ m = 1) (hn : x ^ n = 1) :
    x ^ m.gcd n = 1 := by
  rcases m with (rfl | m); ¬∑ simp [hn]
  -- ‚ä¢ x ^ Nat.gcd Nat.zero n = 1
                             -- üéâ no goals
  obtain ‚ü®y, rfl‚ü© := isUnit_ofPowEqOne hm m.succ_ne_zero
  -- ‚ä¢ ‚Üëy ^ Nat.gcd (Nat.succ m) n = 1
  simp only [‚Üê Units.val_pow_eq_pow_val] at *
  -- ‚ä¢ ‚Üë(y ^ Nat.gcd (Nat.succ m) n) = 1
  rw [‚Üê Units.val_one, ‚Üê zpow_coe_nat, ‚Üê Units.ext_iff] at *
  -- ‚ä¢ y ^ ‚Üë(Nat.gcd (Nat.succ m) n) = 1
  simp only [Nat.gcd_eq_gcd_ab, zpow_add, zpow_mul, hm, hn, one_zpow, one_mul]
  -- üéâ no goals
#align pow_gcd_eq_one pow_gcd_eq_one
#align gcd_nsmul_eq_zero gcd_nsmul_eq_zero
