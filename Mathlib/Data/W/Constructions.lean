/-
Copyright (c) 2015 Joseph Hua. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Hua
-/
import Mathlib.Data.W.Basic

#align_import data.W.constructions from "leanprover-community/mathlib"@"861a26926586cd46ff80264d121cdb6fa0e35cc1"

/-!
# Examples of W-types

We take the view of W types as inductive types.
Given `Î± : Type` and `Î² : Î± â†’ Type`, the W type determined by this data, `WType Î²`, is the
inductively with constructors from `Î±` and arities of each constructor `a : Î±` given by `Î² a`.

This file contains `Nat` and `List` as examples of W types.

## Main results
* `WType.equivNat`: the construction of the naturals as a W-type is equivalent to `Nat`
* `WType.equivList`: the construction of lists on a type `Î³` as a W-type is equivalent to `List Î³`
-/


universe u v

namespace WType

-- For "W_type"
set_option linter.uppercaseLean3 false

section Nat

/-- The constructors for the naturals -/
inductive NatÎ± : Type
  | zero : NatÎ±
  | succ : NatÎ±
#align W_type.nat_Î± WType.NatÎ±

instance : Inhabited NatÎ± :=
  âŸ¨NatÎ±.zeroâŸ©

/-- The arity of the constructors for the naturals, `zero` takes no arguments, `succ` takes one -/
def NatÎ² : NatÎ± â†’ Type
  | NatÎ±.zero => Empty
  | NatÎ±.succ => Unit
#align W_type.nat_Î² WType.NatÎ²

instance : Inhabited (NatÎ² NatÎ±.succ) :=
  âŸ¨()âŸ©

/-- The isomorphism from the naturals to its corresponding `WType` -/
@[simp]
def ofNat : â„• â†’ WType NatÎ²
  | Nat.zero => âŸ¨NatÎ±.zero, Empty.elimâŸ©
  | Nat.succ n => âŸ¨NatÎ±.succ, fun _ â†¦ ofNat nâŸ©
#align W_type.of_nat WType.ofNat

/-- The isomorphism from the `WType` of the naturals to the naturals -/
@[simp]
def toNat : WType NatÎ² â†’ â„•
  | WType.mk NatÎ±.zero _ => 0
  | WType.mk NatÎ±.succ f => (f ()).toNat.succ
#align W_type.to_nat WType.toNat

theorem leftInverse_nat : Function.LeftInverse ofNat toNat
  | WType.mk NatÎ±.zero f => by
    rw [toNat, ofNat]
    -- âŠ¢ mk NatÎ±.zero Empty.elim = mk NatÎ±.zero f
    congr
    -- âŠ¢ Empty.elim = f
    ext x
    -- âŠ¢ Empty.elim x = f x
    cases x
    -- ğŸ‰ no goals
  | WType.mk NatÎ±.succ f => by
    simp only [toNat, ofNat, leftInverse_nat (f ()), mk.injEq, heq_eq_eq, true_and]
    -- âŠ¢ (fun x => f ()) = f
    rfl
    -- ğŸ‰ no goals
#align W_type.left_inv_nat WType.leftInverse_nat

theorem rightInverse_nat : Function.RightInverse ofNat toNat
  | Nat.zero => rfl
  | Nat.succ n => by rw [ofNat, toNat, rightInverse_nat n]
                     -- ğŸ‰ no goals
#align W_type.right_inv_nat WType.rightInverse_nat

/-- The naturals are equivalent to their associated `WType` -/
def equivNat : WType NatÎ² â‰ƒ â„• where
  toFun := toNat
  invFun := ofNat
  left_inv := leftInverse_nat
  right_inv := rightInverse_nat
#align W_type.equiv_nat WType.equivNat

open Sum PUnit

/-- `WType.NatÎ±` is equivalent to `PUnit âŠ• PUnit`.
This is useful when considering the associated polynomial endofunctor.
-/
@[simps]
def NatÎ±EquivPUnitSumPUnit : NatÎ± â‰ƒ Sum PUnit.{u + 1} PUnit
    where
  toFun c :=
    match c with
    | NatÎ±.zero => inl unit
    | NatÎ±.succ => inr unit
  invFun b :=
    match b with
    | inl _ => NatÎ±.zero
    | inr _ => NatÎ±.succ
  left_inv c :=
    match c with
    | NatÎ±.zero => rfl
    | NatÎ±.succ => rfl
  right_inv b :=
    match b with
    | inl _ => rfl
    | inr _ => rfl
#align W_type.nat_Î±_equiv_punit_sum_punit WType.NatÎ±EquivPUnitSumPUnit

end Nat

section List

variable (Î³ : Type u)

/-- The constructors for lists.
There is "one constructor `cons x` for each `x : Î³`",
since we view `List Î³` as
```
| nil : List Î³
| cons xâ‚€ : List Î³ â†’ List Î³
| cons xâ‚ : List Î³ â†’ List Î³
|   â‹®      Î³ many times
```
-/
inductive ListÎ± : Type u
  | nil : ListÎ±
  | cons : Î³ â†’ ListÎ±
#align W_type.list_Î± WType.ListÎ±

instance : Inhabited (ListÎ± Î³) :=
  âŸ¨ListÎ±.nilâŸ©

/-- The arities of each constructor for lists, `nil` takes no arguments, `cons hd` takes one -/
def ListÎ² : ListÎ± Î³ â†’ Type u
  | ListÎ±.nil => PEmpty
  | ListÎ±.cons _ => PUnit
#align W_type.list_Î² WType.ListÎ²

instance (hd : Î³) : Inhabited (ListÎ² Î³ (ListÎ±.cons hd)) :=
  âŸ¨PUnit.unitâŸ©

/-- The isomorphism from lists to the `WType` construction of lists -/
@[simp]
def ofList : List Î³ â†’ WType (ListÎ² Î³)
  | List.nil => âŸ¨ListÎ±.nil, PEmpty.elimâŸ©
  | List.cons hd tl => âŸ¨ListÎ±.cons hd, fun _ â†¦ ofList tlâŸ©
#align W_type.of_list WType.ofList

/-- The isomorphism from the `WType` construction of lists to lists -/
@[simp]
def toList : WType (ListÎ² Î³) â†’ List Î³
  | WType.mk ListÎ±.nil _ => []
  | WType.mk (ListÎ±.cons hd) f => hd :: (f PUnit.unit).toList
#align W_type.to_list WType.toList

theorem leftInverse_list : Function.LeftInverse (ofList Î³) (toList _)
  | WType.mk ListÎ±.nil f => by
    simp only [toList, ofList, mk.injEq, heq_eq_eq, true_and]
    -- âŠ¢ PEmpty.elim = f
    ext x
    -- âŠ¢ PEmpty.elim x = f x
    cases x
    -- ğŸ‰ no goals
  | WType.mk (ListÎ±.cons x) f => by
    simp only [ofList, leftInverse_list (f PUnit.unit), mk.injEq, heq_eq_eq, true_and]
    -- âŠ¢ (fun x => f PUnit.unit) = f
    rfl
    -- ğŸ‰ no goals
#align W_type.left_inv_list WType.leftInverse_list

theorem rightInverse_list : Function.RightInverse (ofList Î³) (toList _)
  | List.nil => rfl
  | List.cons hd tl => by simp only [toList, rightInverse_list tl]
                          -- ğŸ‰ no goals
#align W_type.right_inv_list WType.rightInverse_list

/-- Lists are equivalent to their associated `WType` -/
def equivList : WType (ListÎ² Î³) â‰ƒ List Î³
    where
  toFun := toList _
  invFun := ofList _
  left_inv := leftInverse_list _
  right_inv := rightInverse_list _
#align W_type.equiv_list WType.equivList

/-- `WType.ListÎ±` is equivalent to `Î³` with an extra point.
This is useful when considering the associated polynomial endofunctor
-/
def ListÎ±EquivPUnitSum : ListÎ± Î³ â‰ƒ Sum PUnit.{v + 1} Î³
    where
  toFun c :=
    match c with
    | ListÎ±.nil => Sum.inl PUnit.unit
    | ListÎ±.cons x => Sum.inr x
  invFun := Sum.elim (fun _ â†¦ ListÎ±.nil) ListÎ±.cons
  left_inv c :=
    match c with
    | ListÎ±.nil => rfl
    | ListÎ±.cons _ => rfl
  right_inv x :=
    match x with
    | Sum.inl PUnit.unit => rfl
    | Sum.inr _ => rfl
#align W_type.list_Î±_equiv_punit_sum WType.ListÎ±EquivPUnitSum

end List

end WType
