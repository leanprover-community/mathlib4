/-
Copyright (c) 2019 Amelia Livingston. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Amelia Livingston, Bryan Gin-ge Chen, Patrick Massot
-/
import Mathlib.Data.Fintype.Basic
import Mathlib.Data.Set.Finite
import Mathlib.Data.Setoid.Basic
import Mathlib.Order.Partition.Finpartition

#align_import data.setoid.partition from "leanprover-community/mathlib"@"b363547b3113d350d053abdf2884e9850a56b205"

/-!
# Equivalence relations: partitions

This file comprises properties of equivalence relations viewed as partitions.
There are two implementations of partitions here:
* A collection `c : Set (Set Œ±)` of sets is a partition of `Œ±` if `‚àÖ ‚àâ c` and each element `a : Œ±`
  belongs to a unique set `b ‚àà c`. This is expressed as `IsPartition c`
* An indexed partition is a map `s : Œπ ‚Üí Œ±` whose image is a partition. This is
  expressed as `IndexedPartition s`.

Of course both implementations are related to `Quotient` and `Setoid`.

`Setoid.isPartition.partition` and `Finpartition.isPartition_parts` furnish
a link between `Setoid.IsPartition` and `Finpartition`.

## TODO

Could the design of `Finpartition` inform the one of `Setoid.IsPartition`? Maybe bundling it and
changing it from `Set (Set Œ±)` to `Set Œ±` where `[Lattice Œ±] [OrderBot Œ±]` would make it more
usable.

## Tags

setoid, equivalence, iseqv, relation, equivalence relation, partition, equivalence class
-/


namespace Setoid

variable {Œ± : Type*}

/- ./././Mathport/Syntax/Translate/Basic.lean:628:2:
-- warning: expanding binder collection (b ¬´expr ‚àà ¬ª c) -/
/-- If x ‚àà Œ± is in 2 elements of a set of sets partitioning Œ±, those 2 sets are equal. -/
theorem eq_of_mem_eqv_class {c : Set (Set Œ±)} (H : ‚àÄ a, ‚àÉ! (b : _) (_ : b ‚àà c), a ‚àà b) {x b b'}
    (hc : b ‚àà c) (hb : x ‚àà b) (hc' : b' ‚àà c) (hb' : x ‚àà b') : b = b' :=
  (H x).unique‚ÇÇ hc hb hc' hb'
#align setoid.eq_of_mem_eqv_class Setoid.eq_of_mem_eqv_class

/- ./././Mathport/Syntax/Translate/Basic.lean:628:2:
-- warning: expanding binder collection (b ¬´expr ‚àà ¬ª c) -/
/-- Makes an equivalence relation from a set of sets partitioning Œ±. -/
def mkClasses (c : Set (Set Œ±)) (H : ‚àÄ a, ‚àÉ! (b : _) (_ : b ‚àà c), a ‚àà b) : Setoid Œ± :=
  ‚ü®fun x y => ‚àÄ s ‚àà c, x ‚àà s ‚Üí y ‚àà s,
    ‚ü®fun _ _ _ hx => hx, fun {x _y} h s hs hy =>
      (H x).elim‚ÇÇ fun t ht hx _ =>
        have : s = t := eq_of_mem_eqv_class H hs hy ht (h t ht hx)
        this.symm ‚ñ∏ hx,
      fun {_x y z} h1 h2 s hs hx =>
      (H y).elim‚ÇÇ fun t ht hy _ =>
        (H z).elim‚ÇÇ fun t' ht' hz _ =>
          have hst : s = t := eq_of_mem_eqv_class H hs (h1 _ hs hx) ht hy
          have htt' : t = t' := eq_of_mem_eqv_class H ht (h2 _ ht hy) ht' hz
          (hst.trans htt').symm ‚ñ∏ hz‚ü©‚ü©
#align setoid.mk_classes Setoid.mkClasses

/-- Makes the equivalence classes of an equivalence relation. -/
def classes (r : Setoid Œ±) : Set (Set Œ±) :=
  { s | ‚àÉ y, s = { x | r.Rel x y } }
#align setoid.classes Setoid.classes

theorem mem_classes (r : Setoid Œ±) (y) : { x | r.Rel x y } ‚àà r.classes :=
  ‚ü®y, rfl‚ü©
#align setoid.mem_classes Setoid.mem_classes

theorem classes_ker_subset_fiber_set {Œ≤ : Type*} (f : Œ± ‚Üí Œ≤) :
    (Setoid.ker f).classes ‚äÜ Set.range fun y => { x | f x = y } := by
  rintro s ‚ü®x, rfl‚ü©
  -- ‚ä¢ {x_1 | Rel (ker f) x_1 x} ‚àà Set.range fun y => {x | f x = y}
  rw [Set.mem_range]
  -- ‚ä¢ ‚àÉ y, {x | f x = y} = {x_1 | Rel (ker f) x_1 x}
  exact ‚ü®f x, rfl‚ü©
  -- üéâ no goals
#align setoid.classes_ker_subset_fiber_set Setoid.classes_ker_subset_fiber_set

theorem finite_classes_ker {Œ± Œ≤ : Type*} [Finite Œ≤] (f : Œ± ‚Üí Œ≤) : (Setoid.ker f).classes.Finite :=
  (Set.finite_range _).subset <| classes_ker_subset_fiber_set f
#align setoid.finite_classes_ker Setoid.finite_classes_ker

theorem card_classes_ker_le {Œ± Œ≤ : Type*} [Fintype Œ≤] (f : Œ± ‚Üí Œ≤)
    [Fintype (Setoid.ker f).classes] : Fintype.card (Setoid.ker f).classes ‚â§ Fintype.card Œ≤ := by
  classical exact
      le_trans (Set.card_le_of_subset (classes_ker_subset_fiber_set f)) (Fintype.card_range_le _)
#align setoid.card_classes_ker_le Setoid.card_classes_ker_le

/-- Two equivalence relations are equal iff all their equivalence classes are equal. -/
theorem eq_iff_classes_eq {r‚ÇÅ r‚ÇÇ : Setoid Œ±} :
    r‚ÇÅ = r‚ÇÇ ‚Üî ‚àÄ x, { y | r‚ÇÅ.Rel x y } = { y | r‚ÇÇ.Rel x y } :=
  ‚ü®fun h _x => h ‚ñ∏ rfl, fun h => ext' fun x => Set.ext_iff.1 <| h x‚ü©
#align setoid.eq_iff_classes_eq Setoid.eq_iff_classes_eq

theorem rel_iff_exists_classes (r : Setoid Œ±) {x y} : r.Rel x y ‚Üî ‚àÉ c ‚àà r.classes, x ‚àà c ‚àß y ‚àà c :=
  ‚ü®fun h => ‚ü®_, r.mem_classes y, h, r.refl' y‚ü©, fun ‚ü®c, ‚ü®z, hz‚ü©, hx, hy‚ü© => by
    subst c
    -- ‚ä¢ Rel r x y
    exact r.trans' hx (r.symm' hy)‚ü©
    -- üéâ no goals
#align setoid.rel_iff_exists_classes Setoid.rel_iff_exists_classes

/-- Two equivalence relations are equal iff their equivalence classes are equal. -/
theorem classes_inj {r‚ÇÅ r‚ÇÇ : Setoid Œ±} : r‚ÇÅ = r‚ÇÇ ‚Üî r‚ÇÅ.classes = r‚ÇÇ.classes :=
  ‚ü®fun h => h ‚ñ∏ rfl, fun h => ext' fun a b => by simp only [rel_iff_exists_classes, exists_prop, h]‚ü©
                                                 -- üéâ no goals
#align setoid.classes_inj Setoid.classes_inj

/-- The empty set is not an equivalence class. -/
theorem empty_not_mem_classes {r : Setoid Œ±} : ‚àÖ ‚àâ r.classes := fun ‚ü®y, hy‚ü© =>
  Set.not_mem_empty y <| hy.symm ‚ñ∏ r.refl' y
#align setoid.empty_not_mem_classes Setoid.empty_not_mem_classes

/- ./././Mathport/Syntax/Translate/Basic.lean:628:2:
-- warning: expanding binder collection (b ¬´expr ‚àà ¬ª r.classes) -/
/-- Equivalence classes partition the type. -/
theorem classes_eqv_classes {r : Setoid Œ±} (a) : ‚àÉ! (b : _) (_ : b ‚àà r.classes), a ‚àà b :=
  ExistsUnique.intro‚ÇÇ { x | r.Rel x a } (r.mem_classes a) (r.refl' _) <| by
    rintro _ ‚ü®y, rfl‚ü© ha
    -- ‚ä¢ {x | Rel r x y} = {x | Rel r x a}
    ext x
    -- ‚ä¢ x ‚àà {x | Rel r x y} ‚Üî x ‚àà {x | Rel r x a}
    exact ‚ü®fun hx => r.trans' hx (r.symm' ha), fun hx => r.trans' hx ha‚ü©
    -- üéâ no goals
#align setoid.classes_eqv_classes Setoid.classes_eqv_classes

/-- If x ‚àà Œ± is in 2 equivalence classes, the equivalence classes are equal. -/
theorem eq_of_mem_classes {r : Setoid Œ±} {x b} (hc : b ‚àà r.classes) (hb : x ‚àà b) {b'}
    (hc' : b' ‚àà r.classes) (hb' : x ‚àà b') : b = b' :=
  eq_of_mem_eqv_class classes_eqv_classes hc hb hc' hb'
#align setoid.eq_of_mem_classes Setoid.eq_of_mem_classes

/- ./././Mathport/Syntax/Translate/Basic.lean:628:2:
-- warning: expanding binder collection (b ¬´expr ‚àà ¬ª c) -/
/-- The elements of a set of sets partitioning Œ± are the equivalence classes of the
    equivalence relation defined by the set of sets. -/
theorem eq_eqv_class_of_mem {c : Set (Set Œ±)} (H : ‚àÄ a, ‚àÉ! (b : _) (_ : b ‚àà c), a ‚àà b) {s y}
    (hs : s ‚àà c) (hy : y ‚àà s) : s = { x | (mkClasses c H).Rel x y } :=
  Set.ext fun x =>
    ‚ü®fun hs' => symm' (mkClasses c H) fun _b' hb' h' => eq_of_mem_eqv_class H hs hy hb' h' ‚ñ∏ hs',
      fun hx =>
      (H x).elim‚ÇÇ fun b' hc' hb' _h' =>
        (eq_of_mem_eqv_class H hs hy hc' <| hx b' hc' hb').symm ‚ñ∏ hb'‚ü©
#align setoid.eq_eqv_class_of_mem Setoid.eq_eqv_class_of_mem

/- ./././Mathport/Syntax/Translate/Basic.lean:628:2:
-- warning: expanding binder collection (b ¬´expr ‚àà ¬ª c) -/
/-- The equivalence classes of the equivalence relation defined by a set of sets
    partitioning Œ± are elements of the set of sets. -/
theorem eqv_class_mem {c : Set (Set Œ±)} (H : ‚àÄ a, ‚àÉ! (b : _) (_ : b ‚àà c), a ‚àà b) {y} :
    { x | (mkClasses c H).Rel x y } ‚àà c :=
  (H y).elim‚ÇÇ fun _b hc hy _hb => eq_eqv_class_of_mem H hc hy ‚ñ∏ hc
#align setoid.eqv_class_mem Setoid.eqv_class_mem

/- ./././Mathport/Syntax/Translate/Basic.lean:628:2:
-- warning: expanding binder collection (b ¬´expr ‚àà ¬ª c) -/
theorem eqv_class_mem' {c : Set (Set Œ±)} (H : ‚àÄ a, ‚àÉ! (b : _) (_ : b ‚àà c), a ‚àà b) {x} :
    { y : Œ± | (mkClasses c H).Rel x y } ‚àà c := by
  convert @Setoid.eqv_class_mem _ _ H x using 3
  -- ‚ä¢ Rel (mkClasses c H) x x‚úù ‚Üî Rel (mkClasses c H) x‚úù x
  rw [Setoid.comm']
  -- üéâ no goals
#align setoid.eqv_class_mem' Setoid.eqv_class_mem'

/- ./././Mathport/Syntax/Translate/Basic.lean:628:2:
-- warning: expanding binder collection (b ¬´expr ‚àà ¬ª c) -/
/-- Distinct elements of a set of sets partitioning Œ± are disjoint. -/
theorem eqv_classes_disjoint {c : Set (Set Œ±)} (H : ‚àÄ a, ‚àÉ! (b : _) (_ : b ‚àà c), a ‚àà b) :
    c.PairwiseDisjoint id := fun _b‚ÇÅ h‚ÇÅ _b‚ÇÇ h‚ÇÇ h =>
  Set.disjoint_left.2 fun x hx1 hx2 =>
    (H x).elim‚ÇÇ fun _b _hc _hx _hb => h <| eq_of_mem_eqv_class H h‚ÇÅ hx1 h‚ÇÇ hx2
#align setoid.eqv_classes_disjoint Setoid.eqv_classes_disjoint

/- ./././Mathport/Syntax/Translate/Basic.lean:628:2:
-- warning: expanding binder collection (b ¬´expr ‚àà ¬ª c) -/
/-- A set of disjoint sets covering Œ± partition Œ± (classical). -/
theorem eqv_classes_of_disjoint_union {c : Set (Set Œ±)} (hu : Set.sUnion c = @Set.univ Œ±)
    (H : c.PairwiseDisjoint id) (a) : ‚àÉ! (b : _) (_ : b ‚àà c), a ‚àà b :=
  let ‚ü®b, hc, ha‚ü© := Set.mem_sUnion.1 <| show a ‚àà _ by rw [hu]; exact Set.mem_univ a
                                                       -- ‚ä¢ a ‚àà Set.univ
                                                                -- üéâ no goals
  ExistsUnique.intro‚ÇÇ b hc ha fun b' hc' ha' => H.elim_set hc' hc a ha' ha
#align setoid.eqv_classes_of_disjoint_union Setoid.eqv_classes_of_disjoint_union

/-- Makes an equivalence relation from a set of disjoints sets covering Œ±. -/
def setoidOfDisjointUnion {c : Set (Set Œ±)} (hu : Set.sUnion c = @Set.univ Œ±)
    (H : c.PairwiseDisjoint id) : Setoid Œ± :=
  Setoid.mkClasses c <| eqv_classes_of_disjoint_union hu H
#align setoid.setoid_of_disjoint_union Setoid.setoidOfDisjointUnion

/-- The equivalence relation made from the equivalence classes of an equivalence
    relation r equals r. -/
theorem mkClasses_classes (r : Setoid Œ±) : mkClasses r.classes classes_eqv_classes = r :=
  ext' fun x _y =>
    ‚ü®fun h => r.symm' (h { z | r.Rel z x } (r.mem_classes x) <| r.refl' x), fun h _b hb hx =>
      eq_of_mem_classes (r.mem_classes x) (r.refl' x) hb hx ‚ñ∏ r.symm' h‚ü©
#align setoid.mk_classes_classes Setoid.mkClasses_classes

@[simp]
theorem sUnion_classes (r : Setoid Œ±) : ‚ãÉ‚ÇÄ r.classes = Set.univ :=
  Set.eq_univ_of_forall fun x => Set.mem_sUnion.2 ‚ü®{ y | r.Rel y x }, ‚ü®x, rfl‚ü©, Setoid.refl _‚ü©
#align setoid.sUnion_classes Setoid.sUnion_classes

section Partition

/- ./././Mathport/Syntax/Translate/Basic.lean:628:2:
-- warning: expanding binder collection (b ¬´expr ‚àà ¬ª c) -/
/-- A collection `c : Set (Set Œ±)` of sets is a partition of `Œ±` into pairwise
disjoint sets if `‚àÖ ‚àâ c` and each element `a : Œ±` belongs to a unique set `b ‚àà c`. -/
def IsPartition (c : Set (Set Œ±)) :=
  ‚àÖ ‚àâ c ‚àß ‚àÄ a, ‚àÉ! (b : _) (_ : b ‚àà c), a ‚àà b
#align setoid.is_partition Setoid.IsPartition

/-- A partition of `Œ±` does not contain the empty set. -/
theorem nonempty_of_mem_partition {c : Set (Set Œ±)} (hc : IsPartition c) {s} (h : s ‚àà c) :
    s.Nonempty :=
  Set.nonempty_iff_ne_empty.2 fun hs0 => hc.1 <| hs0 ‚ñ∏ h
#align setoid.nonempty_of_mem_partition Setoid.nonempty_of_mem_partition

theorem isPartition_classes (r : Setoid Œ±) : IsPartition r.classes :=
  ‚ü®empty_not_mem_classes, classes_eqv_classes‚ü©
#align setoid.is_partition_classes Setoid.isPartition_classes

theorem IsPartition.pairwiseDisjoint {c : Set (Set Œ±)} (hc : IsPartition c) :
    c.PairwiseDisjoint id :=
  eqv_classes_disjoint hc.2
#align setoid.is_partition.pairwise_disjoint Setoid.IsPartition.pairwiseDisjoint

theorem IsPartition.sUnion_eq_univ {c : Set (Set Œ±)} (hc : IsPartition c) : ‚ãÉ‚ÇÄ c = Set.univ :=
  Set.eq_univ_of_forall fun x =>
    Set.mem_sUnion.2 <|
      let ‚ü®t, ht‚ü© := hc.2 x
      ‚ü®t, by
        simp only [exists_unique_iff_exists] at ht
        -- ‚ä¢ t ‚àà c ‚àß x ‚àà t
        tauto‚ü©
        -- üéâ no goals
#align setoid.is_partition.sUnion_eq_univ Setoid.IsPartition.sUnion_eq_univ

/-- All elements of a partition of Œ± are the equivalence class of some y ‚àà Œ±. -/
theorem exists_of_mem_partition {c : Set (Set Œ±)} (hc : IsPartition c) {s} (hs : s ‚àà c) :
    ‚àÉ y, s = { x | (mkClasses c hc.2).Rel x y } :=
  let ‚ü®y, hy‚ü© := nonempty_of_mem_partition hc hs
  ‚ü®y, eq_eqv_class_of_mem hc.2 hs hy‚ü©
#align setoid.exists_of_mem_partition Setoid.exists_of_mem_partition

/-- The equivalence classes of the equivalence relation defined by a partition of Œ± equal
    the original partition. -/
theorem classes_mkClasses (c : Set (Set Œ±)) (hc : IsPartition c) : (mkClasses c hc.2).classes = c :=
  Set.ext fun s => ‚ü®fun ‚ü®y, hs‚ü© => (hc.2 y).elim‚ÇÇ fun b hm hb _hy => by
    rwa [show s = b from hs.symm ‚ñ∏ Set.ext fun x =>
      ‚ü®fun hx => symm' (mkClasses c hc.2) hx b hm hb, fun hx b' hc' hx' =>
          eq_of_mem_eqv_class hc.2 hm hx hc' hx' ‚ñ∏ hb‚ü©],
    exists_of_mem_partition hc‚ü©
#align setoid.classes_mk_classes Setoid.classes_mkClasses

/-- Defining `‚â§` on partitions as the `‚â§` defined on their induced equivalence relations. -/
instance Partition.le : LE (Subtype (@IsPartition Œ±)) :=
  ‚ü®fun x y => mkClasses x.1 x.2.2 ‚â§ mkClasses y.1 y.2.2‚ü©
#align setoid.partition.le Setoid.Partition.le

/-- Defining a partial order on partitions as the partial order on their induced
    equivalence relations. -/
instance Partition.partialOrder : PartialOrder (Subtype (@IsPartition Œ±))
    where
  le := (¬∑ ‚â§ ¬∑)
  lt x y := x ‚â§ y ‚àß ¬¨y ‚â§ x
  le_refl _ := @le_refl (Setoid Œ±) _ _
  le_trans _ _ _ := @le_trans (Setoid Œ±) _ _ _ _
  lt_iff_le_not_le _ _ := Iff.rfl
  le_antisymm x y hx hy := by
    let h := @le_antisymm (Setoid Œ±) _ _ _ hx hy
    -- ‚ä¢ x = y
    rw [Subtype.ext_iff_val, ‚Üê classes_mkClasses x.1 x.2, ‚Üê classes_mkClasses y.1 y.2, h]
    -- üéâ no goals
#align setoid.partition.partial_order Setoid.Partition.partialOrder

variable (Œ±)

/-- The order-preserving bijection between equivalence relations on a type `Œ±`, and
  partitions of `Œ±` into subsets. -/
protected def Partition.orderIso : Setoid Œ± ‚âÉo { C : Set (Set Œ±) // IsPartition C }
    where
  toFun r := ‚ü®r.classes, empty_not_mem_classes, classes_eqv_classes‚ü©
  invFun C := mkClasses C.1 C.2.2
  left_inv := mkClasses_classes
  right_inv C := by rw [Subtype.ext_iff_val, ‚Üê classes_mkClasses C.1 C.2]
                    -- üéâ no goals
  map_rel_iff' {r s} := by
    conv_rhs => rw [‚Üê mkClasses_classes r, ‚Üê mkClasses_classes s]
    -- üéâ no goals
#align setoid.partition.order_iso Setoid.Partition.orderIso

variable {Œ±}

/-- A complete lattice instance for partitions; there is more infrastructure for the
    equivalent complete lattice on equivalence relations. -/
instance Partition.completeLattice : CompleteLattice (Subtype (@IsPartition Œ±)) :=
  GaloisInsertion.liftCompleteLattice <|
    @OrderIso.toGaloisInsertion _ (Subtype (@IsPartition Œ±)) _ (PartialOrder.toPreorder) <|
      Partition.orderIso Œ±
#align setoid.partition.complete_lattice Setoid.Partition.completeLattice

end Partition

/-- A finite setoid partition furnishes a finpartition -/
@[simps]
def IsPartition.finpartition {c : Finset (Set Œ±)} (hc : Setoid.IsPartition (c : Set (Set Œ±))) :
    Finpartition (Set.univ : Set Œ±) where
  parts := c
  supIndep := Finset.supIndep_iff_pairwiseDisjoint.mpr <| eqv_classes_disjoint hc.2
  supParts := c.sup_id_set_eq_sUnion.trans hc.sUnion_eq_univ
  not_bot_mem := hc.left
#align setoid.is_partition.finpartition Setoid.IsPartition.finpartition

end Setoid

/-- A finpartition gives rise to a setoid partition -/
theorem Finpartition.isPartition_parts {Œ±} (f : Finpartition (Set.univ : Set Œ±)) :
    Setoid.IsPartition (f.parts : Set (Set Œ±)) :=
  ‚ü®f.not_bot_mem,
    Setoid.eqv_classes_of_disjoint_union (f.parts.sup_id_set_eq_sUnion.symm.trans f.supParts)
      f.supIndep.pairwiseDisjoint‚ü©
#align finpartition.is_partition_parts Finpartition.isPartition_parts

/-- Constructive information associated with a partition of a type `Œ±` indexed by another type `Œπ`,
`s : Œπ ‚Üí Set Œ±`.

`IndexedPartition.index` sends an element to its index, while `IndexedPartition.some` sends
an index to an element of the corresponding set.

This type is primarily useful for definitional control of `s` - if this is not needed, then
`Setoid.ker index` by itself may be sufficient. -/
structure IndexedPartition {Œπ Œ± : Type*} (s : Œπ ‚Üí Set Œ±) where
  /-- two indexes are equal if they are equal in membership  -/
  eq_of_mem : ‚àÄ {x i j}, x ‚àà s i ‚Üí x ‚àà s j ‚Üí i = j
  /-- sends an index to an element of the corresponding set-/
  some : Œπ ‚Üí Œ±
  /-- membership invariance for `some`-/
  some_mem : ‚àÄ i, some i ‚àà s i
  /-- index for type `Œ±`-/
  index : Œ± ‚Üí Œπ
  /-- membership invariance for `index`-/
  mem_index : ‚àÄ x, x ‚àà s (index x)
#align indexed_partition IndexedPartition

/-- The non-constructive constructor for `IndexedPartition`. -/
noncomputable def IndexedPartition.mk' {Œπ Œ± : Type*} (s : Œπ ‚Üí Set Œ±)
    (dis : ‚àÄ i j, i ‚â† j ‚Üí Disjoint (s i) (s j)) (nonempty : ‚àÄ i, (s i).Nonempty)
    (ex : ‚àÄ x, ‚àÉ i, x ‚àà s i) : IndexedPartition s
    where
  eq_of_mem {_x _i _j} hxi hxj := by_contradiction fun h => (dis _ _ h).le_bot ‚ü®hxi, hxj‚ü©
  some i := (nonempty i).some
  some_mem i := (nonempty i).choose_spec
  index x := (ex x).choose
  mem_index x := (ex x).choose_spec
#align indexed_partition.mk' IndexedPartition.mk'

namespace IndexedPartition

open Set

variable {Œπ Œ± : Type*} {s : Œπ ‚Üí Set Œ±} (hs : IndexedPartition s)

/-- On a unique index set there is the obvious trivial partition -/
instance [Unique Œπ] [Inhabited Œ±] : Inhabited (IndexedPartition fun _i : Œπ => (Set.univ : Set Œ±)) :=
  ‚ü®{  eq_of_mem := fun {_x _i _j} _hi _hj => Subsingleton.elim _ _
      some := default
      some_mem := Set.mem_univ
      index := default
      mem_index := Set.mem_univ }‚ü©

-- porting note: `simpNF` complains about `mem_index`
attribute [simp] some_mem --mem_index

theorem exists_mem (x : Œ±) : ‚àÉ i, x ‚àà s i :=
  ‚ü®hs.index x, hs.mem_index x‚ü©
#align indexed_partition.exists_mem IndexedPartition.exists_mem

theorem iUnion : ‚ãÉ i, s i = univ := by
  ext x
  -- ‚ä¢ x ‚àà ‚ãÉ (i : Œπ), s i ‚Üî x ‚àà univ
  simp [hs.exists_mem x]
  -- üéâ no goals
#align indexed_partition.Union IndexedPartition.iUnion

theorem disjoint : ‚àÄ {i j}, i ‚â† j ‚Üí Disjoint (s i) (s j) := fun {_i _j} h =>
  disjoint_left.mpr fun {_x} hxi hxj => h (hs.eq_of_mem hxi hxj)
#align indexed_partition.disjoint IndexedPartition.disjoint

theorem mem_iff_index_eq {x i} : x ‚àà s i ‚Üî hs.index x = i :=
  ‚ü®fun hxi => (hs.eq_of_mem hxi (hs.mem_index x)).symm, fun h => h ‚ñ∏ hs.mem_index _‚ü©
#align indexed_partition.mem_iff_index_eq IndexedPartition.mem_iff_index_eq

theorem eq (i) : s i = { x | hs.index x = i } :=
  Set.ext fun _ => hs.mem_iff_index_eq
#align indexed_partition.eq IndexedPartition.eq

/-- The equivalence relation associated to an indexed partition. Two
elements are equivalent if they belong to the same set of the partition. -/
protected abbrev setoid (hs : IndexedPartition s) : Setoid Œ± :=
  Setoid.ker hs.index
#align indexed_partition.setoid IndexedPartition.setoid

@[simp]
theorem index_some (i : Œπ) : hs.index (hs.some i) = i :=
  (mem_iff_index_eq _).1 <| hs.some_mem i
#align indexed_partition.index_some IndexedPartition.index_some

theorem some_index (x : Œ±) : hs.setoid.Rel (hs.some (hs.index x)) x :=
  hs.index_some (hs.index x)
#align indexed_partition.some_index IndexedPartition.some_index

/-- The quotient associated to an indexed partition. -/
protected def Quotient :=
  Quotient hs.setoid
#align indexed_partition.quotient IndexedPartition.Quotient

/-- The projection onto the quotient associated to an indexed partition. -/
def proj : Œ± ‚Üí hs.Quotient :=
  Quotient.mk''
#align indexed_partition.proj IndexedPartition.proj

instance [Inhabited Œ±] : Inhabited hs.Quotient :=
  ‚ü®hs.proj default‚ü©

theorem proj_eq_iff {x y : Œ±} : hs.proj x = hs.proj y ‚Üî hs.index x = hs.index y :=
  Quotient.eq_rel
#align indexed_partition.proj_eq_iff IndexedPartition.proj_eq_iff

@[simp]
theorem proj_some_index (x : Œ±) : hs.proj (hs.some (hs.index x)) = hs.proj x :=
  Quotient.eq''.2 (hs.some_index x)
#align indexed_partition.proj_some_index IndexedPartition.proj_some_index

/-- The obvious equivalence between the quotient associated to an indexed partition and
the indexing type. -/
def equivQuotient : Œπ ‚âÉ hs.Quotient :=
  (Setoid.quotientKerEquivOfRightInverse hs.index hs.some <| hs.index_some).symm
#align indexed_partition.equiv_quotient IndexedPartition.equivQuotient

@[simp]
theorem equivQuotient_index_apply (x : Œ±) : hs.equivQuotient (hs.index x) = hs.proj x :=
  hs.proj_eq_iff.mpr (some_index hs x)
#align indexed_partition.equiv_quotient_index_apply IndexedPartition.equivQuotient_index_apply

@[simp]
theorem equivQuotient_symm_proj_apply (x : Œ±) : hs.equivQuotient.symm (hs.proj x) = hs.index x :=
  rfl
#align indexed_partition.equiv_quotient_symm_proj_apply IndexedPartition.equivQuotient_symm_proj_apply

theorem equivQuotient_index : hs.equivQuotient ‚àò hs.index = hs.proj :=
  funext hs.equivQuotient_index_apply
#align indexed_partition.equiv_quotient_index IndexedPartition.equivQuotient_index

/-- A map choosing a representative for each element of the quotient associated to an indexed
partition. This is a computable version of `Quotient.out'` using `IndexedPartition.some`. -/
def out : hs.Quotient ‚Ü™ Œ± :=
  hs.equivQuotient.symm.toEmbedding.trans ‚ü®hs.some, Function.LeftInverse.injective hs.index_some‚ü©
#align indexed_partition.out IndexedPartition.out

/-- This lemma is analogous to `Quotient.mk_out'`. -/
@[simp]
theorem out_proj (x : Œ±) : hs.out (hs.proj x) = hs.some (hs.index x) :=
  rfl
#align indexed_partition.out_proj IndexedPartition.out_proj

/-- The indices of `Quotient.out'` and `IndexedPartition.out` are equal. -/
theorem index_out' (x : hs.Quotient) : hs.index x.out' = hs.index (hs.out x) :=
  Quotient.inductionOn' x fun x => (Setoid.ker_apply_mk_out' x).trans (hs.index_some _).symm
#align indexed_partition.index_out' IndexedPartition.index_out'

/-- This lemma is analogous to `Quotient.out_eq'`. -/
@[simp]
theorem proj_out (x : hs.Quotient) : hs.proj (hs.out x) = x :=
  Quotient.inductionOn' x fun x => Quotient.sound' <| hs.some_index x
#align indexed_partition.proj_out IndexedPartition.proj_out

theorem class_of {x : Œ±} : setOf (hs.setoid.Rel x) = s (hs.index x) :=
  Set.ext fun _y => eq_comm.trans hs.mem_iff_index_eq.symm
#align indexed_partition.class_of IndexedPartition.class_of

theorem proj_fiber (x : hs.Quotient) : hs.proj ‚Åª¬π' {x} = s (hs.equivQuotient.symm x) :=
  Quotient.inductionOn' x fun x => by
    ext y
    -- ‚ä¢ y ‚àà proj hs ‚Åª¬π' {Quotient.mk'' x} ‚Üî y ‚àà s (‚Üë(equivQuotient hs).symm (Quotien ‚Ä¶
    simp only [Set.mem_preimage, Set.mem_singleton_iff, hs.mem_iff_index_eq]
    -- ‚ä¢ proj hs y = Quotient.mk'' x ‚Üî index hs y = ‚Üë(equivQuotient hs).symm (Quotien ‚Ä¶
    exact Quotient.eq''
    -- üéâ no goals
#align indexed_partition.proj_fiber IndexedPartition.proj_fiber

end IndexedPartition
