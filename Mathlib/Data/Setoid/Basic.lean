/-
Copyright (c) 2019 Amelia Livingston. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Amelia Livingston, Bryan Gin-ge Chen
-/
import Mathlib.Logic.Relation
import Mathlib.Order.GaloisConnection

#align_import data.setoid.basic from "leanprover-community/mathlib"@"bbeb185db4ccee8ed07dc48449414ebfa39cb821"

/-!
# Equivalence relations

This file defines the complete lattice of equivalence relations on a type, results about the
inductively defined equivalence closure of a binary relation, and the analogues of some isomorphism
theorems for quotients of arbitrary types.

## Implementation notes

The function `Rel` and lemmas ending in ' make it easier to talk about different
equivalence relations on the same type.

The complete lattice instance for equivalence relations could have been defined by lifting
the Galois insertion of equivalence relations on Œ± into binary relations on Œ±, and then using
`CompleteLattice.copy` to define a complete lattice instance with more appropriate
definitional equalities (a similar example is `Filter.CompleteLattice` in
`Order/Filter/Basic.lean`). This does not save space, however, and is less clear.

Partitions are not defined as a separate structure here; users are encouraged to
reason about them using the existing `Setoid` and its infrastructure.

## Tags

setoid, equivalence, iseqv, relation, equivalence relation
-/

variable {Œ± : Type*} {Œ≤ : Type*}

/-- A version of `Setoid.r` that takes the equivalence relation as an explicit argument. -/
def Setoid.Rel (r : Setoid Œ±) : Œ± ‚Üí Œ± ‚Üí Prop :=
  @Setoid.r _ r
#align setoid.rel Setoid.Rel

instance Setoid.decidableRel (r : Setoid Œ±) [h : DecidableRel r.r] : DecidableRel r.Rel :=
  h
#align setoid.decidable_rel Setoid.decidableRel

/-- A version of `Quotient.eq'` compatible with `Setoid.Rel`, to make rewriting possible. -/
theorem Quotient.eq_rel {r : Setoid Œ±} {x y} :
    (Quotient.mk' x : Quotient r) = Quotient.mk' y ‚Üî r.Rel x y :=
  Quotient.eq
#align quotient.eq_rel Quotient.eq_rel

namespace Setoid

@[ext]
theorem ext' {r s : Setoid Œ±} (H : ‚àÄ a b, r.Rel a b ‚Üî s.Rel a b) : r = s :=
  ext H
#align setoid.ext' Setoid.ext'

theorem ext_iff {r s : Setoid Œ±} : r = s ‚Üî ‚àÄ a b, r.Rel a b ‚Üî s.Rel a b :=
  ‚ü®fun h _ _ => h ‚ñ∏ Iff.rfl, ext'‚ü©
#align setoid.ext_iff Setoid.ext_iff

/-- Two equivalence relations are equal iff their underlying binary operations are equal. -/
theorem eq_iff_rel_eq {r‚ÇÅ r‚ÇÇ : Setoid Œ±} : r‚ÇÅ = r‚ÇÇ ‚Üî r‚ÇÅ.Rel = r‚ÇÇ.Rel :=
  ‚ü®fun h => h ‚ñ∏ rfl, fun h => Setoid.ext' fun _ _ => h ‚ñ∏ Iff.rfl‚ü©
#align setoid.eq_iff_rel_eq Setoid.eq_iff_rel_eq

/-- Defining `‚â§` for equivalence relations. -/
instance : LE (Setoid Œ±) :=
  ‚ü®fun r s => ‚àÄ ‚¶Éx y‚¶Ñ, r.Rel x y ‚Üí s.Rel x y‚ü©

theorem le_def {r s : Setoid Œ±} : r ‚â§ s ‚Üî ‚àÄ {x y}, r.Rel x y ‚Üí s.Rel x y :=
  Iff.rfl
#align setoid.le_def Setoid.le_def

@[refl]
theorem refl' (r : Setoid Œ±) (x) : r.Rel x x := r.iseqv.refl x
#align setoid.refl' Setoid.refl'

@[symm]
theorem symm' (r : Setoid Œ±) : ‚àÄ {x y}, r.Rel x y ‚Üí r.Rel y x := r.iseqv.symm
#align setoid.symm' Setoid.symm'

@[trans]
theorem trans' (r : Setoid Œ±) : ‚àÄ {x y z}, r.Rel x y ‚Üí r.Rel y z ‚Üí r.Rel x z := r.iseqv.trans
#align setoid.trans' Setoid.trans'

theorem comm' (s : Setoid Œ±) {x y} : s.Rel x y ‚Üî s.Rel y x :=
  ‚ü®s.symm', s.symm'‚ü©
#align setoid.comm' Setoid.comm'

/-- The kernel of a function is an equivalence relation. -/
def ker (f : Œ± ‚Üí Œ≤) : Setoid Œ± :=
  ‚ü®(¬∑ = ¬∑) on f, eq_equivalence.comap f‚ü©
#align setoid.ker Setoid.ker

/-- The kernel of the quotient map induced by an equivalence relation r equals r. -/
@[simp]
theorem ker_mk_eq (r : Setoid Œ±) : ker (@Quotient.mk'' _ r) = r :=
  ext' fun _ _ => Quotient.eq
#align setoid.ker_mk_eq Setoid.ker_mk_eq

theorem ker_apply_mk_out {f : Œ± ‚Üí Œ≤} (a : Œ±) : f (haveI := Setoid.ker f; ‚ü¶a‚üß.out) = f a :=
  @Quotient.mk_out _ (Setoid.ker f) a
#align setoid.ker_apply_mk_out Setoid.ker_apply_mk_out

theorem ker_apply_mk_out' {f : Œ± ‚Üí Œ≤} (a : Œ±) :
    f (Quotient.mk _ a : Quotient <| Setoid.ker f).out' = f a :=
  @Quotient.mk_out' _ (Setoid.ker f) a
#align setoid.ker_apply_mk_out' Setoid.ker_apply_mk_out'

theorem ker_def {f : Œ± ‚Üí Œ≤} {x y : Œ±} : (ker f).Rel x y ‚Üî f x = f y :=
  Iff.rfl
#align setoid.ker_def Setoid.ker_def

/-- Given types `Œ±`, `Œ≤`, the product of two equivalence relations `r` on `Œ±` and `s` on `Œ≤`:
    `(x‚ÇÅ, x‚ÇÇ), (y‚ÇÅ, y‚ÇÇ) ‚àà Œ± √ó Œ≤` are related by `r.prod s` iff `x‚ÇÅ` is related to `y‚ÇÅ`
    by `r` and `x‚ÇÇ` is related to `y‚ÇÇ` by `s`. -/
protected def prod (r : Setoid Œ±) (s : Setoid Œ≤) :
    Setoid (Œ± √ó Œ≤) where
  r x y := r.Rel x.1 y.1 ‚àß s.Rel x.2 y.2
  iseqv :=
    ‚ü®fun x => ‚ü®r.refl' x.1, s.refl' x.2‚ü©, fun h => ‚ü®r.symm' h.1, s.symm' h.2‚ü©,
      fun h‚ÇÅ h‚ÇÇ => ‚ü®r.trans' h‚ÇÅ.1 h‚ÇÇ.1, s.trans' h‚ÇÅ.2 h‚ÇÇ.2‚ü©‚ü©
#align setoid.prod Setoid.prod

/-- The infimum of two equivalence relations. -/
instance : Inf (Setoid Œ±) :=
  ‚ü®fun r s =>
    ‚ü®fun x y => r.Rel x y ‚àß s.Rel x y,
      ‚ü®fun x => ‚ü®r.refl' x, s.refl' x‚ü©, fun h => ‚ü®r.symm' h.1, s.symm' h.2‚ü©, fun h1 h2 =>
        ‚ü®r.trans' h1.1 h2.1, s.trans' h1.2 h2.2‚ü©‚ü©‚ü©‚ü©

/-- The infimum of 2 equivalence relations r and s is the same relation as the infimum
    of the underlying binary operations. -/
theorem inf_def {r s : Setoid Œ±} : (r ‚äì s).Rel = r.Rel ‚äì s.Rel :=
  rfl
#align setoid.inf_def Setoid.inf_def

theorem inf_iff_and {r s : Setoid Œ±} {x y} : (r ‚äì s).Rel x y ‚Üî r.Rel x y ‚àß s.Rel x y :=
  Iff.rfl
#align setoid.inf_iff_and Setoid.inf_iff_and

/-- The infimum of a set of equivalence relations. -/
instance : InfSet (Setoid Œ±) :=
  ‚ü®fun S =>
    { r := fun x y => ‚àÄ r ‚àà S, r.Rel x y
      iseqv := ‚ü®fun x r _ => r.refl' x, fun h r hr => r.symm' <| h r hr, fun h1 h2 r hr =>
        r.trans' (h1 r hr) <| h2 r hr‚ü© }‚ü©

/-- The underlying binary operation of the infimum of a set of equivalence relations
    is the infimum of the set's image under the map to the underlying binary operation. -/
theorem sInf_def {s : Set (Setoid Œ±)} : (sInf s).Rel = sInf (Rel '' s) := by
  ext
  -- ‚ä¢ Rel (sInf s) x‚úù¬π x‚úù ‚Üî sInf (Rel '' s) x‚úù¬π x‚úù
  simp only [sInf_image, iInf_apply, iInf_Prop_eq]
  -- ‚ä¢ Rel (sInf s) x‚úù¬π x‚úù ‚Üî ‚àÄ (i : Setoid Œ±), i ‚àà s ‚Üí Rel i x‚úù¬π x‚úù
  rfl
  -- üéâ no goals
#align setoid.Inf_def Setoid.sInf_def

instance : PartialOrder (Setoid Œ±) where
  le := (¬∑ ‚â§ ¬∑)
  lt r s := r ‚â§ s ‚àß ¬¨s ‚â§ r
  le_refl _ _ _ := id
  le_trans _ _ _ hr hs _ _ h := hs <| hr h
  lt_iff_le_not_le _ _ := Iff.rfl
  le_antisymm _ _ h1 h2 := Setoid.ext' fun _ _ => ‚ü®fun h => h1 h, fun h => h2 h‚ü©

/-- The complete lattice of equivalence relations on a type, with bottom element `=`
    and top element the trivial equivalence relation. -/
instance completeLattice : CompleteLattice (Setoid Œ±) :=
  { (completeLatticeOfInf (Setoid Œ±)) fun _ =>
      ‚ü®fun _ hr _ _ h => h _ hr, fun _ hr _ _ h _ hr' => hr hr' h‚ü© with
    inf := Inf.inf
    inf_le_left := fun _ _ _ _ h => h.1
    inf_le_right := fun _ _ _ _ h => h.2
    le_inf := fun _ _ _ h1 h2 _ _ h => ‚ü®h1 h, h2 h‚ü©
    top := ‚ü®fun _ _ => True, ‚ü®fun _ => trivial, fun h => h, fun h1 _ => h1‚ü©‚ü©
    le_top := fun _ _ _ _ => trivial
    bot := ‚ü®(¬∑ = ¬∑), ‚ü®fun _ => rfl, fun h => h.symm, fun h1 h2 => h1.trans h2‚ü©‚ü©
    bot_le := fun r x _ h => h ‚ñ∏ r.2.1 x }
#align setoid.complete_lattice Setoid.completeLattice

@[simp]
theorem top_def : (‚ä§ : Setoid Œ±).Rel = ‚ä§ :=
  rfl
#align setoid.top_def Setoid.top_def

@[simp]
theorem bot_def : (‚ä• : Setoid Œ±).Rel = (¬∑ = ¬∑) :=
  rfl
#align setoid.bot_def Setoid.bot_def

theorem eq_top_iff {s : Setoid Œ±} : s = (‚ä§ : Setoid Œ±) ‚Üî ‚àÄ x y : Œ±, s.Rel x y := by
  rw [_root_.eq_top_iff, Setoid.le_def, Setoid.top_def]
  -- ‚ä¢ (‚àÄ {x y : Œ±}, ‚ä§ x y ‚Üí Rel s x y) ‚Üî ‚àÄ (x y : Œ±), Rel s x y
  simp only [Pi.top_apply, Prop.top_eq_true, forall_true_left]
  -- üéâ no goals
#align setoid.eq_top_iff Setoid.eq_top_iff

/-- The inductively defined equivalence closure of a binary relation r is the infimum
    of the set of all equivalence relations containing r. -/
theorem eqvGen_eq (r : Œ± ‚Üí Œ± ‚Üí Prop) :
    EqvGen.Setoid r = sInf { s : Setoid Œ± | ‚àÄ ‚¶Éx y‚¶Ñ, r x y ‚Üí s.Rel x y } :=
  le_antisymm
    (fun _ _ H =>
      EqvGen.rec (fun _ _ h _ hs => hs h) (refl' _) (fun _ _ _ => symm' _)
        (fun _ _ _ _ _ => trans' _) H)
    (sInf_le fun _ _ h => EqvGen.rel _ _ h)
#align setoid.eqv_gen_eq Setoid.eqvGen_eq

/-- The supremum of two equivalence relations r and s is the equivalence closure of the binary
    relation `x is related to y by r or s`. -/
theorem sup_eq_eqvGen (r s : Setoid Œ±) :
    r ‚äî s = EqvGen.Setoid fun x y => r.Rel x y ‚à® s.Rel x y := by
  rw [eqvGen_eq]
  -- ‚ä¢ r ‚äî s = sInf {s_1 | ‚àÄ ‚¶Éx y : Œ±‚¶Ñ, Rel r x y ‚à® Rel s x y ‚Üí Rel s_1 x y}
  apply congr_arg sInf
  -- ‚ä¢ {x | r ‚â§ x ‚àß s ‚â§ x} = {s_1 | ‚àÄ ‚¶Éx y : Œ±‚¶Ñ, Rel r x y ‚à® Rel s x y ‚Üí Rel s_1 x y}
  simp only [le_def, or_imp, ‚Üê forall_and]
  -- üéâ no goals
#align setoid.sup_eq_eqv_gen Setoid.sup_eq_eqvGen

/-- The supremum of 2 equivalence relations r and s is the equivalence closure of the
    supremum of the underlying binary operations. -/
theorem sup_def {r s : Setoid Œ±} : r ‚äî s = EqvGen.Setoid (r.Rel ‚äî s.Rel) := by
  rw [sup_eq_eqvGen]; rfl
  -- ‚ä¢ (EqvGen.Setoid fun x y => Rel r x y ‚à® Rel s x y) = EqvGen.Setoid (Rel r ‚äî Re ‚Ä¶
                      -- üéâ no goals
#align setoid.sup_def Setoid.sup_def

/-- The supremum of a set S of equivalence relations is the equivalence closure of the binary
    relation `there exists r ‚àà S relating x and y`. -/
theorem sSup_eq_eqvGen (S : Set (Setoid Œ±)) :
    sSup S = EqvGen.Setoid fun x y => ‚àÉ r : Setoid Œ±, r ‚àà S ‚àß r.Rel x y := by
  rw [eqvGen_eq]
  -- ‚ä¢ sSup S = sInf {s | ‚àÄ ‚¶Éx y : Œ±‚¶Ñ, (‚àÉ r, r ‚àà S ‚àß Rel r x y) ‚Üí Rel s x y}
  apply congr_arg sInf
  -- ‚ä¢ upperBounds S = {s | ‚àÄ ‚¶Éx y : Œ±‚¶Ñ, (‚àÉ r, r ‚àà S ‚àß Rel r x y) ‚Üí Rel s x y}
  simp only [upperBounds, le_def, and_imp, exists_imp]
  -- ‚ä¢ {x | ‚àÄ ‚¶Éa : Setoid Œ±‚¶Ñ, a ‚àà S ‚Üí ‚àÄ {x_1 y : Œ±}, Rel a x_1 y ‚Üí Rel x x_1 y} = { ‚Ä¶
  ext
  -- ‚ä¢ x‚úù ‚àà {x | ‚àÄ ‚¶Éa : Setoid Œ±‚¶Ñ, a ‚àà S ‚Üí ‚àÄ {x_1 y : Œ±}, Rel a x_1 y ‚Üí Rel x x_1 y ‚Ä¶
  exact ‚ü®fun H x y r hr => H hr, fun H r hr x y => H r hr‚ü©
  -- üéâ no goals
#align setoid.Sup_eq_eqv_gen Setoid.sSup_eq_eqvGen

/-- The supremum of a set of equivalence relations is the equivalence closure of the
    supremum of the set's image under the map to the underlying binary operation. -/
theorem sSup_def {s : Set (Setoid Œ±)} : sSup s = EqvGen.Setoid (sSup (Rel '' s)) := by
  rw [sSup_eq_eqvGen, sSup_image]
  -- ‚ä¢ (EqvGen.Setoid fun x y => ‚àÉ r, r ‚àà s ‚àß Rel r x y) = EqvGen.Setoid (‚®Ü (a : Se ‚Ä¶
  congr with (x y)
  -- ‚ä¢ (‚àÉ r, r ‚àà s ‚àß Rel r x y) ‚Üî iSup (fun a => ‚®Ü (_ : a ‚àà s), Rel a) x y
  simp only [iSup_apply, iSup_Prop_eq, exists_prop]
  -- üéâ no goals
#align setoid.Sup_def Setoid.sSup_def

/-- The equivalence closure of an equivalence relation r is r. -/
@[simp]
theorem eqvGen_of_setoid (r : Setoid Œ±) : EqvGen.Setoid r.r = r :=
  le_antisymm (by rw [eqvGen_eq]; exact sInf_le fun _ _ => id) EqvGen.rel
                  -- ‚ä¢ sInf {s | ‚àÄ ‚¶Éx y : Œ±‚¶Ñ, Setoid.r x y ‚Üí Rel s x y} ‚â§ r
                                  -- üéâ no goals
#align setoid.eqv_gen_of_setoid Setoid.eqvGen_of_setoid

/-- Equivalence closure is idempotent. -/
@[simp]
theorem eqvGen_idem (r : Œ± ‚Üí Œ± ‚Üí Prop) : EqvGen.Setoid (EqvGen.Setoid r).Rel = EqvGen.Setoid r :=
  eqvGen_of_setoid _
#align setoid.eqv_gen_idem Setoid.eqvGen_idem

/-- The equivalence closure of a binary relation r is contained in any equivalence
    relation containing r. -/
theorem eqvGen_le {r : Œ± ‚Üí Œ± ‚Üí Prop} {s : Setoid Œ±} (h : ‚àÄ x y, r x y ‚Üí s.Rel x y) :
    EqvGen.Setoid r ‚â§ s := by rw [eqvGen_eq]; exact sInf_le h
                              -- ‚ä¢ sInf {s | ‚àÄ ‚¶Éx y : Œ±‚¶Ñ, r x y ‚Üí Rel s x y} ‚â§ s
                                              -- üéâ no goals
#align setoid.eqv_gen_le Setoid.eqvGen_le

/-- Equivalence closure of binary relations is monotone. -/
theorem eqvGen_mono {r s : Œ± ‚Üí Œ± ‚Üí Prop} (h : ‚àÄ x y, r x y ‚Üí s x y) :
    EqvGen.Setoid r ‚â§ EqvGen.Setoid s :=
  eqvGen_le fun _ _ hr => EqvGen.rel _ _ <| h _ _ hr
#align setoid.eqv_gen_mono Setoid.eqvGen_mono

/-- There is a Galois insertion of equivalence relations on Œ± into binary relations
    on Œ±, with equivalence closure the lower adjoint. -/
def gi : @GaloisInsertion (Œ± ‚Üí Œ± ‚Üí Prop) (Setoid Œ±) _ _ EqvGen.Setoid Rel where
  choice r _ := EqvGen.Setoid r
  gc _ s := ‚ü®fun H _ _ h => H <| EqvGen.rel _ _ h, fun H => eqvGen_of_setoid s ‚ñ∏ eqvGen_mono H‚ü©
  le_l_u x := (eqvGen_of_setoid x).symm ‚ñ∏ le_refl x
  choice_eq _ _ := rfl
#align setoid.gi Setoid.gi

open Function

/-- A function from Œ± to Œ≤ is injective iff its kernel is the bottom element of the complete lattice
    of equivalence relations on Œ±. -/
theorem injective_iff_ker_bot (f : Œ± ‚Üí Œ≤) : Injective f ‚Üî ker f = ‚ä• :=
  (@eq_bot_iff (Setoid Œ±) _ _ (ker f)).symm
#align setoid.injective_iff_ker_bot Setoid.injective_iff_ker_bot

/-- The elements related to x ‚àà Œ± by the kernel of f are those in the preimage of f(x) under f. -/
theorem ker_iff_mem_preimage {f : Œ± ‚Üí Œ≤} {x y} : (ker f).Rel x y ‚Üî x ‚àà f ‚Åª¬π' {f y} :=
  Iff.rfl
#align setoid.ker_iff_mem_preimage Setoid.ker_iff_mem_preimage

/-- Equivalence between functions `Œ± ‚Üí Œ≤` such that `r x y ‚Üí f x = f y` and functions
`quotient r ‚Üí Œ≤`. -/
def liftEquiv (r : Setoid Œ±) : { f : Œ± ‚Üí Œ≤ // r ‚â§ ker f } ‚âÉ (Quotient r ‚Üí Œ≤) where
  toFun f := Quotient.lift (f : Œ± ‚Üí Œ≤) f.2
  invFun f := ‚ü®f ‚àò Quotient.mk'', fun x y h => by simp [ker_def, Quotient.sound' h]‚ü©
                                                  -- üéâ no goals
  left_inv := fun ‚ü®f, hf‚ü© => Subtype.eq <| funext fun x => rfl
  right_inv f := funext fun x => Quotient.inductionOn' x fun x => rfl
#align setoid.lift_equiv Setoid.liftEquiv

/-- The uniqueness part of the universal property for quotients of an arbitrary type. -/
theorem lift_unique {r : Setoid Œ±} {f : Œ± ‚Üí Œ≤} (H : r ‚â§ ker f) (g : Quotient r ‚Üí Œ≤)
    (Hg : f = g ‚àò Quotient.mk'') : Quotient.lift f H = g := by
  ext ‚ü®x‚ü©
  -- ‚ä¢ Quotient.lift f H (Quot.mk Setoid.r x) = g (Quot.mk Setoid.r x)
  erw [Quotient.lift_mk f H, Hg]
  -- ‚ä¢ (g ‚àò Quotient.mk'') x = g (Quot.mk Setoid.r x)
  rfl
  -- üéâ no goals
#align setoid.lift_unique Setoid.lift_unique

/-- Given a map f from Œ± to Œ≤, the natural map from the quotient of Œ± by the kernel of f is
    injective. -/
theorem ker_lift_injective (f : Œ± ‚Üí Œ≤) : Injective (@Quotient.lift _ _ (ker f) f fun _ _ h => h) :=
  fun x y => Quotient.inductionOn‚ÇÇ' x y fun _ _ h => Quotient.sound' h
#align setoid.ker_lift_injective Setoid.ker_lift_injective

/-- Given a map f from Œ± to Œ≤, the kernel of f is the unique equivalence relation on Œ± whose
    induced map from the quotient of Œ± to Œ≤ is injective. -/
theorem ker_eq_lift_of_injective {r : Setoid Œ±} (f : Œ± ‚Üí Œ≤) (H : ‚àÄ x y, r.Rel x y ‚Üí f x = f y)
    (h : Injective (Quotient.lift f H)) : ker f = r :=
  le_antisymm
    (fun x y hk =>
      Quotient.exact <| h <| show Quotient.lift f H ‚ü¶x‚üß = Quotient.lift f H ‚ü¶y‚üß from hk)
    H
#align setoid.ker_eq_lift_of_injective Setoid.ker_eq_lift_of_injective

variable (r : Setoid Œ±) (f : Œ± ‚Üí Œ≤)

/-- The first isomorphism theorem for sets: the quotient of Œ± by the kernel of a function f
    bijects with f's image. -/
noncomputable def quotientKerEquivRange : Quotient (ker f) ‚âÉ Set.range f :=
  Equiv.ofBijective
    ((@Quotient.lift _ (Set.range f) (ker f) fun x => ‚ü®f x, Set.mem_range_self x‚ü©) fun _ _ h =>
      Subtype.ext_val h)
    ‚ü®fun x y h => ker_lift_injective f <| by rcases x with ‚ü®‚ü©; rcases y with ‚ü®‚ü©; injections,
                                             -- ‚ä¢ Quotient.lift f (_ : ‚àÄ (x x_1 : Œ±), x ‚âà x_1 ‚Üí x ‚âà x_1) (Quot.mk Setoid.r a‚úù) ‚Ä¶
                                                               -- ‚ä¢ Quotient.lift f (_ : ‚àÄ (x x_1 : Œ±), x ‚âà x_1 ‚Üí x ‚âà x_1) (Quot.mk Setoid.r a‚úù¬π ‚Ä¶
                                                                                 -- üéâ no goals
      fun ‚ü®w, z, hz‚ü© =>
      ‚ü®@Quotient.mk'' _ (ker f) z, Subtype.ext_iff_val.2 hz‚ü©‚ü©
#align setoid.quotient_ker_equiv_range Setoid.quotientKerEquivRange

/-- If `f` has a computable right-inverse, then the quotient by its kernel is equivalent to its
domain. -/
@[simps]
def quotientKerEquivOfRightInverse (g : Œ≤ ‚Üí Œ±) (hf : Function.RightInverse g f) :
    Quotient (ker f) ‚âÉ Œ≤ where
  toFun a := (Quotient.liftOn' a f) fun _ _ => id
  invFun b := Quotient.mk'' (g b)
  left_inv a := Quotient.inductionOn' a fun a => Quotient.sound' <| hf (f a)
  right_inv := hf
#align setoid.quotient_ker_equiv_of_right_inverse Setoid.quotientKerEquivOfRightInverse
#align setoid.quotient_ker_equiv_of_right_inverse_symm_apply Setoid.quotientKerEquivOfRightInverse_symm_apply
#align setoid.quotient_ker_equiv_of_right_inverse_apply Setoid.quotientKerEquivOfRightInverse_apply

/-- The quotient of Œ± by the kernel of a surjective function f bijects with f's codomain.

If a specific right-inverse of `f` is known, `Setoid.quotientKerEquivOfRightInverse` can be
definitionally more useful. -/
noncomputable def quotientKerEquivOfSurjective (hf : Surjective f) : Quotient (ker f) ‚âÉ Œ≤ :=
  quotientKerEquivOfRightInverse _ (Function.surjInv hf) (rightInverse_surjInv hf)
#align setoid.quotient_ker_equiv_of_surjective Setoid.quotientKerEquivOfSurjective

variable {r f}

/-- Given a function `f : Œ± ‚Üí Œ≤` and equivalence relation `r` on `Œ±`, the equivalence
    closure of the relation on `f`'s image defined by '`x ‚âà y` iff the elements of `f‚Åª¬π(x)` are
    related to the elements of `f‚Åª¬π(y)` by `r`.' -/
def map (r : Setoid Œ±) (f : Œ± ‚Üí Œ≤) : Setoid Œ≤ :=
  EqvGen.Setoid fun x y => ‚àÉ a b, f a = x ‚àß f b = y ‚àß r.Rel a b
#align setoid.map Setoid.map

/-- Given a surjective function f whose kernel is contained in an equivalence relation r, the
    equivalence relation on f's codomain defined by x ‚âà y ‚Üî the elements of f‚Åª¬π(x) are related to
    the elements of f‚Åª¬π(y) by r. -/
def mapOfSurjective (r) (f : Œ± ‚Üí Œ≤) (h : ker f ‚â§ r) (hf : Surjective f) : Setoid Œ≤ :=
  ‚ü®fun x y => ‚àÉ a b, f a = x ‚àß f b = y ‚àß r.Rel a b,
    ‚ü®fun x =>
      let ‚ü®y, hy‚ü© := hf x
      ‚ü®y, y, hy, hy, r.refl' y‚ü©,
      fun ‚ü®x, y, hx, hy, h‚ü© => ‚ü®y, x, hy, hx, r.symm' h‚ü©,
      fun ‚ü®x, y, hx, hy, h‚ÇÅ‚ü© ‚ü®y', z, hy', hz, h‚ÇÇ‚ü© =>
      ‚ü®x, z, hx, hz, r.trans' h‚ÇÅ <| r.trans' (h <| by rwa [‚Üê hy'] at hy) h‚ÇÇ‚ü©‚ü©‚ü©
                                                      -- üéâ no goals
#align setoid.map_of_surjective Setoid.mapOfSurjective

/-- A special case of the equivalence closure of an equivalence relation r equalling r. -/
theorem mapOfSurjective_eq_map (h : ker f ‚â§ r) (hf : Surjective f) :
    map r f = mapOfSurjective r f h hf := by
  rw [‚Üê eqvGen_of_setoid (mapOfSurjective r f h hf)]; rfl
  -- ‚ä¢ map r f = EqvGen.Setoid Setoid.r
                                                      -- üéâ no goals
#align setoid.map_of_surjective_eq_map Setoid.mapOfSurjective_eq_map

/-- Given a function `f : Œ± ‚Üí Œ≤`, an equivalence relation `r` on `Œ≤` induces an equivalence
relation on `Œ±` defined by '`x ‚âà y` iff `f(x)` is related to `f(y)` by `r`'.

See note [reducible non-instances]. -/
@[reducible]
def comap (f : Œ± ‚Üí Œ≤) (r : Setoid Œ≤) : Setoid Œ± :=
  ‚ü®r.Rel on f, r.iseqv.comap _‚ü©
#align setoid.comap Setoid.comap

theorem comap_rel (f : Œ± ‚Üí Œ≤) (r : Setoid Œ≤) (x y : Œ±) : (comap f r).Rel x y ‚Üî r.Rel (f x) (f y) :=
  Iff.rfl
#align setoid.comap_rel Setoid.comap_rel

/-- Given a map `f : N ‚Üí M` and an equivalence relation `r` on `Œ≤`, the equivalence relation
    induced on `Œ±` by `f` equals the kernel of `r`'s quotient map composed with `f`. -/
theorem comap_eq {f : Œ± ‚Üí Œ≤} {r : Setoid Œ≤} : comap f r = ker (@Quotient.mk'' _ r ‚àò f) :=
  ext fun x y => show _ ‚Üî ‚ü¶_‚üß = ‚ü¶_‚üß by rw [Quotient.eq]; rfl
                                       -- ‚ä¢ Setoid.r x y ‚Üî f x ‚âà f y
                                                         -- üéâ no goals
#align setoid.comap_eq Setoid.comap_eq

/-- The second isomorphism theorem for sets. -/
noncomputable def comapQuotientEquiv (f : Œ± ‚Üí Œ≤) (r : Setoid Œ≤) :
    Quotient (comap f r) ‚âÉ Set.range (@Quotient.mk'' _ r ‚àò f) :=
  (Quotient.congrRight <| ext_iff.1 comap_eq).trans <| quotientKerEquivRange <| Quotient.mk'' ‚àò f
#align setoid.comap_quotient_equiv Setoid.comapQuotientEquiv

variable (r f)

/-- The third isomorphism theorem for sets. -/
def quotientQuotientEquivQuotient (s : Setoid Œ±) (h : r ‚â§ s) :
    Quotient (ker (Quot.mapRight h)) ‚âÉ Quotient s where
  toFun x :=
    (Quotient.liftOn' x fun w =>
        (Quotient.liftOn' w (@Quotient.mk'' _ s)) fun x y H => Quotient.sound <| h H)
      fun x y => Quotient.inductionOn‚ÇÇ' x y fun w z H => show @Quot.mk _ _ _ = @Quot.mk _ _ _ from H
  invFun x :=
    (Quotient.liftOn' x fun w => @Quotient.mk'' _ (ker <| Quot.mapRight h) <| @Quotient.mk'' _ r w)
      fun x y H => Quotient.sound' <| show @Quot.mk _ _ _ = @Quot.mk _ _ _ from Quotient.sound H
  left_inv x :=
    Quotient.inductionOn' x fun y => Quotient.inductionOn' y fun w => by show ‚ü¶_‚üß = _; rfl
                                                                         -- ‚ä¢ Quotient.mk (ker (Quot.mapRight h)) (Quotient.mk'' w) = Quotient.mk'' (Quoti ‚Ä¶
                                                                                       -- üéâ no goals
  right_inv x := Quotient.inductionOn' x fun y => by show ‚ü¶_‚üß = _; rfl
                                                     -- ‚ä¢ Quotient.mk s y = Quotient.mk'' y
                                                                   -- üéâ no goals
#align setoid.quotient_quotient_equiv_quotient Setoid.quotientQuotientEquivQuotient

variable {r f}

open Quotient

/-- Given an equivalence relation `r` on `Œ±`, the order-preserving bijection between the set of
equivalence relations containing `r` and the equivalence relations on the quotient of `Œ±` by `r`. -/
def correspondence (r : Setoid Œ±) : { s // r ‚â§ s } ‚âÉo Setoid (Quotient r) where
  toFun s := mapOfSurjective s.1 Quotient.mk'' ((ker_mk_eq r).symm ‚ñ∏ s.2) exists_rep
  invFun s := ‚ü®comap Quotient.mk' s, fun x y h => by rw [comap_rel, eq_rel.2 h]‚ü©
                                                     -- üéâ no goals
  left_inv s := by
    ext
    -- ‚ä¢ Rel (‚Üë((fun s => { val := comap Quotient.mk' s, property := (_ : ‚àÄ (x y : Œ±) ‚Ä¶
    refine ‚ü®?_, fun h => ‚ü®_, _, rfl, rfl, h‚ü©‚ü©
    -- ‚ä¢ Rel (‚Üë((fun s => { val := comap Quotient.mk' s, property := (_ : ‚àÄ (x y : Œ±) ‚Ä¶
    intro ‚ü®a, b, hx, hy, H‚ü©
    -- ‚ä¢ Rel (‚Üës) a‚úù b‚úù
    refine s.1.trans' (s.1.symm' <| s.2 <| eq_rel.1 hx) (s.1.trans' H <| s.2 <| (eq_rel.1 hy))
    -- üéâ no goals
  right_inv s :=
    ext' fun x y =>
      ‚ü®fun h => let ‚ü®_, _, hx, hy, H‚ü© := h; hx ‚ñ∏ hy ‚ñ∏ H,
        Quotient.inductionOn‚ÇÇ x y fun w z h => ‚ü®w, z, rfl, rfl, h‚ü©‚ü©
  map_rel_iff' := by
    intro s t
    -- ‚ä¢ ‚Üë{ toFun := fun s => mapOfSurjective (‚Üës) Quotient.mk'' (_ : ker Quotient.mk ‚Ä¶
    refine ‚ü®?_, ?_‚ü©
    -- ‚ä¢ ‚Üë{ toFun := fun s => mapOfSurjective (‚Üës) Quotient.mk'' (_ : ker Quotient.mk ‚Ä¶
    ¬∑ intro h x y hs
      -- ‚ä¢ Rel (‚Üët) x y
      let ‚ü®a, b, hx, hy, ht‚ü© := h ‚ü®x, y, rfl, rfl, hs‚ü©
      -- ‚ä¢ Rel (‚Üët) x y
      exact t.1.trans' (t.1.symm' <| t.2 <| eq_rel.1 hx) <| t.1.trans' ht <| t.2 <| eq_rel.1 hy
      -- üéâ no goals
    ¬∑ intro h x y hs
      -- ‚ä¢ Rel (‚Üë{ toFun := fun s => mapOfSurjective (‚Üës) Quotient.mk'' (_ : ker Quotie ‚Ä¶
      let ‚ü®a, b, hx, hy, Hs‚ü© := hs
      -- ‚ä¢ Rel (‚Üë{ toFun := fun s => mapOfSurjective (‚Üës) Quotient.mk'' (_ : ker Quotie ‚Ä¶
      exact ‚ü®a, b, hx, hy, h Hs‚ü©
      -- üéâ no goals
#align setoid.correspondence Setoid.correspondence

end Setoid

@[simp]
theorem Quotient.subsingleton_iff {s : Setoid Œ±} : Subsingleton (Quotient s) ‚Üî s = ‚ä§ := by
  simp only [_root_.subsingleton_iff, eq_top_iff, Setoid.le_def, Setoid.top_def, Pi.top_apply,
    forall_const]
  refine' (surjective_quotient_mk _).forall.trans (forall_congr' fun a => _)
  -- ‚ä¢ (‚àÄ (y : Quotient s), Quotient.mk s a = y) ‚Üî ‚àÄ {y : Œ±}, ‚ä§ ‚Üí Setoid.Rel s a y
  refine' (surjective_quotient_mk _).forall.trans (forall_congr' fun b => _)
  -- ‚ä¢ Quotient.mk s a = Quotient.mk s b ‚Üî ‚ä§ ‚Üí Setoid.Rel s a b
  simp_rw [‚ÜêQuotient.mk''_eq_mk, Prop.top_eq_true, true_implies, Quotient.eq'']
  -- ‚ä¢ Setoid.r a b ‚Üî Setoid.Rel s a b
  rfl
  -- üéâ no goals
#align quotient.subsingleton_iff Quotient.subsingleton_iff

theorem Quot.subsingleton_iff (r : Œ± ‚Üí Œ± ‚Üí Prop) : Subsingleton (Quot r) ‚Üî EqvGen r = ‚ä§ := by
  simp only [_root_.subsingleton_iff, _root_.eq_top_iff, Pi.le_def, Pi.top_apply, forall_const]
  -- ‚ä¢ (‚àÄ (x y : Quot r), x = y) ‚Üî ‚àÄ (i i_1 : Œ±), ‚ä§ ‚â§ EqvGen r i i_1
  refine' (surjective_quot_mk _).forall.trans (forall_congr' fun a => _)
  -- ‚ä¢ (‚àÄ (y : Quot r), mk r a = y) ‚Üî ‚àÄ (i : Œ±), ‚ä§ ‚â§ EqvGen r a i
  refine' (surjective_quot_mk _).forall.trans (forall_congr' fun b => _)
  -- ‚ä¢ mk r a = mk r b ‚Üî ‚ä§ ‚â§ EqvGen r a b
  rw [Quot.eq]
  -- ‚ä¢ EqvGen r a b ‚Üî ‚ä§ ‚â§ EqvGen r a b
  simp only [forall_const, le_Prop_eq, OrderTop.toTop, Pi.orderTop, Pi.top_apply,
             Prop.top_eq_true, true_implies]
#align quot.subsingleton_iff Quot.subsingleton_iff
