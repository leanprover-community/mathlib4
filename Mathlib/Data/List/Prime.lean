/-
Copyright (c) 2018 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Jens Wagemaker, Anne Baanen
-/
import Mathlib.Algebra.Associated
import Mathlib.Data.List.BigOperators.Lemmas
import Mathlib.Data.List.Perm

#align_import data.list.prime from "leanprover-community/mathlib"@"ccad6d5093bd2f5c6ca621fc74674cce51355af6"

/-!
# Products of lists of prime elements.

This file contains some theorems relating `Prime` and products of `List`s.

-/


open List

section CommMonoidWithZero

variable {M : Type*} [CommMonoidWithZero M]

/-- Prime `p` divides the product of a list `L` iff it divides some `a ‚àà L` -/
theorem Prime.dvd_prod_iff {p : M} {L : List M} (pp : Prime p) : p ‚à£ L.prod ‚Üî ‚àÉ a ‚àà L, p ‚à£ a := by
  constructor
  -- ‚ä¢ p ‚à£ prod L ‚Üí ‚àÉ a, a ‚àà L ‚àß p ‚à£ a
  ¬∑ intro h
    -- ‚ä¢ ‚àÉ a, a ‚àà L ‚àß p ‚à£ a
    induction' L with L_hd L_tl L_ih
    -- ‚ä¢ ‚àÉ a, a ‚àà [] ‚àß p ‚à£ a
    ¬∑ rw [prod_nil] at h
      -- ‚ä¢ ‚àÉ a, a ‚àà [] ‚àß p ‚à£ a
      exact absurd h pp.not_dvd_one
      -- üéâ no goals
    ¬∑ rw [prod_cons] at h
      -- ‚ä¢ ‚àÉ a, a ‚àà L_hd :: L_tl ‚àß p ‚à£ a
      cases' pp.dvd_or_dvd h with hd hd
      -- ‚ä¢ ‚àÉ a, a ‚àà L_hd :: L_tl ‚àß p ‚à£ a
      ¬∑ exact ‚ü®L_hd, mem_cons_self L_hd L_tl, hd‚ü©
        -- üéâ no goals
      ¬∑ obtain ‚ü®x, hx1, hx2‚ü© := L_ih hd
        -- ‚ä¢ ‚àÉ a, a ‚àà L_hd :: L_tl ‚àß p ‚à£ a
        exact ‚ü®x, mem_cons_of_mem L_hd hx1, hx2‚ü©
        -- üéâ no goals
  ¬∑ exact fun ‚ü®a, ha1, ha2‚ü© => dvd_trans ha2 (dvd_prod ha1)
    -- üéâ no goals
#align prime.dvd_prod_iff Prime.dvd_prod_iff

theorem Prime.not_dvd_prod {p : M} {L : List M} (pp : Prime p) (hL : ‚àÄ a ‚àà L, ¬¨p ‚à£ a) :
    ¬¨p ‚à£ L.prod :=
  mt (Prime.dvd_prod_iff pp).1 <| not_exists.2 <| fun a => not_and.2 (hL a)
#align prime.not_dvd_prod Prime.not_dvd_prod

end CommMonoidWithZero

section CancelCommMonoidWithZero

variable {M : Type*} [CancelCommMonoidWithZero M] [Unique (Units M)]

theorem mem_list_primes_of_dvd_prod {p : M} (hp : Prime p) {L : List M} (hL : ‚àÄ q ‚àà L, Prime q)
    (hpL : p ‚à£ L.prod) : p ‚àà L := by
  obtain ‚ü®x, hx1, hx2‚ü© := hp.dvd_prod_iff.mp hpL
  -- ‚ä¢ p ‚àà L
  rwa [(prime_dvd_prime_iff_eq hp (hL x hx1)).mp hx2]
  -- üéâ no goals
#align mem_list_primes_of_dvd_prod mem_list_primes_of_dvd_prod

theorem perm_of_prod_eq_prod :
    ‚àÄ {l‚ÇÅ l‚ÇÇ : List M}, l‚ÇÅ.prod = l‚ÇÇ.prod ‚Üí (‚àÄ p ‚àà l‚ÇÅ, Prime p) ‚Üí (‚àÄ p ‚àà l‚ÇÇ, Prime p) ‚Üí Perm l‚ÇÅ l‚ÇÇ
  | [], [], _, _, _ => Perm.nil
  | [], a :: l, h‚ÇÅ, _, h‚ÇÉ =>
    have ha : a ‚à£ 1 := @prod_nil M _ ‚ñ∏ h‚ÇÅ.symm ‚ñ∏ (@prod_cons _ _ l a).symm ‚ñ∏ dvd_mul_right _ _
    absurd ha (Prime.not_dvd_one (h‚ÇÉ a (mem_cons_self _ _)))
  | a :: l, [], h‚ÇÅ, h‚ÇÇ, _ =>
    have ha : a ‚à£ 1 := @prod_nil M _ ‚ñ∏ h‚ÇÅ ‚ñ∏ (@prod_cons _ _ l a).symm ‚ñ∏ dvd_mul_right _ _
    absurd ha (Prime.not_dvd_one (h‚ÇÇ a (mem_cons_self _ _)))
  | a :: l‚ÇÅ, b :: l‚ÇÇ, h, hl‚ÇÅ, hl‚ÇÇ => by
    classical
      have hl‚ÇÅ' : ‚àÄ p ‚àà l‚ÇÅ, Prime p := fun p hp => hl‚ÇÅ p (mem_cons_of_mem _ hp)
      have hl‚ÇÇ' : ‚àÄ p ‚àà (b :: l‚ÇÇ).erase a, Prime p := fun p hp => hl‚ÇÇ p (mem_of_mem_erase hp)
      have ha : a ‚àà b :: l‚ÇÇ :=
        mem_list_primes_of_dvd_prod (hl‚ÇÅ a (mem_cons_self _ _)) hl‚ÇÇ
          (h ‚ñ∏ by rw [prod_cons]; exact dvd_mul_right _ _)
      have hb : b :: l‚ÇÇ ~ a :: (b :: l‚ÇÇ).erase a := perm_cons_erase ha
      have hl : prod l‚ÇÅ = prod ((b :: l‚ÇÇ).erase a) :=
        (mul_right_inj' (hl‚ÇÅ a (mem_cons_self _ _)).ne_zero).1 <| by
          rwa [‚Üê prod_cons, ‚Üê prod_cons, ‚Üê hb.prod_eq]
      exact Perm.trans ((perm_of_prod_eq_prod hl hl‚ÇÅ' hl‚ÇÇ').cons _) hb.symm
#align perm_of_prod_eq_prod perm_of_prod_eq_prod

end CancelCommMonoidWithZero
