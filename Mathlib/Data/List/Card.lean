/-
Authors: Jeremy Avigad

This is a makeshift theory of the cardinality of a list. Any list can be taken to represent the
finite set of its elements. Cardinality counts the number of distinct elements. Cardinality
respects equivalence and is preserved by any mapping that is injective on its elements.

It might make sense to remove this when we have a proper theory of finite sets.
-/
import Mathlib.Data.List.Basic

set_option autoImplicit true

namespace List

def inj_on (f : Î± â†’ Î²) (as : List Î±) := âˆ€ {x y}, x âˆˆ as â†’ y âˆˆ as â†’ f x = f y â†’ x = y

theorem inj_on_of_subset {f : Î± â†’ Î²} {as bs : List Î±} (h : inj_on f bs) (hsub : as âŠ† bs) :
  inj_on f as := fun xas yas heq â†¦ h (hsub xas) (hsub yas) heq

protected def equiv (as bs : List Î±) := âˆ€ x, x âˆˆ as â†” x âˆˆ bs

theorem equiv_iff_subset_and_subset {as bs : List Î±} : as.equiv bs â†” as âŠ† bs âˆ§ bs âŠ† as :=
  Iff.intro
    (fun h â†¦ âŸ¨fun _ xas â†¦ (h _).1 xas, fun _ xbs â†¦ (h _).2 xbsâŸ©)
    (fun âŸ¨h1, h2âŸ© x â†¦ âŸ¨@h1 x, @h2 xâŸ©)

theorem insert_equiv_cons [DecidableEq Î±] (a : Î±) (as : List Î±) : (as.insert a).equiv (a :: as) :=
  fun x â†¦ by simp
             -- ðŸŽ‰ no goals

theorem union_equiv_append [DecidableEq Î±] (as bs : List Î±) : (as âˆª bs).equiv (as ++ bs) :=
  fun x â†¦ by simp
             -- ðŸŽ‰ no goals

section DecidableEq
variable [DecidableEq Î±] [DecidableEq Î²]

/- remove -/

def remove (a : Î±) : List Î± â†’ List Î±
  | [] => []
  | (b :: bs) => if a = b then remove a bs else b :: remove a bs

theorem mem_remove_iff {a b : Î±} {as : List Î±} : b âˆˆ remove a as â†” b âˆˆ as âˆ§ b â‰  a := by
  induction as with
  | nil => simp [remove]
  | cons a' as ih =>
    simp [remove]
    cases Decidable.em (a = a') with
    | inl h =>
      simp only [if_pos h, ih]
      exact âŸ¨fun âŸ¨h1, h2âŸ© â†¦ âŸ¨Or.inr h1, h2âŸ©, fun âŸ¨h1, h2âŸ© â†¦ âŸ¨Or.resolve_left h1 (h â–¸ h2), h2âŸ©âŸ©
    | inr h =>
      simp [if_neg h, ih]
      constructor
      { focus
        intro h'
        cases h' with
        | inl hâ‚ => exact âŸ¨Or.inl hâ‚, hâ‚.symm â–¸ (Ne.symm h)âŸ©
        | inr hâ‚ => exact âŸ¨Or.inr hâ‚.1, hâ‚.2âŸ© }
      intro âŸ¨h1, h2âŸ©
      cases h1 with
      | inl h1' => exact Or.inl h1'
      | inr h1' => exact Or.inr âŸ¨h1', h2âŸ©

theorem remove_eq_of_not_mem {a : Î±} : âˆ€ {as : List Î±}, (a âˆ‰ as) â†’ remove a as = as
  | [], _ => by simp [remove]
                -- ðŸŽ‰ no goals
  | a' :: as, h => by
    have h1 : a â‰  a' := fun h' â†¦ h (by rw [h']; apply mem_cons_self)
    -- âŠ¢ remove a (a' :: as) = a' :: as
    have h2 : a âˆ‰ as := fun h' â†¦ h (mem_cons_of_mem _ h')
    -- âŠ¢ remove a (a' :: as) = a' :: as
    simp [remove, h1, remove_eq_of_not_mem h2]
    -- ðŸŽ‰ no goals

theorem mem_of_mem_remove {a b : Î±} {as : List Î±} (h : b âˆˆ remove a as) : b âˆˆ as := by
  rw [mem_remove_iff] at h; exact h.1
  -- âŠ¢ b âˆˆ as
                            -- ðŸŽ‰ no goals

/- card -/

def card : List Î± â†’ Nat
  | [] => 0
  | a :: as => if a âˆˆ as then card as else card as + 1

@[simp] theorem card_nil : card ([] : List Î±) = 0 := rfl

@[simp] theorem card_cons_of_mem {a : Î±} {as : List Î±} (h : a âˆˆ as) :
    card (a :: as) = card as := by simp [card, h]
                                   -- ðŸŽ‰ no goals

@[simp] theorem card_cons_of_not_mem {a : Î±} {as : List Î±} (h : a âˆ‰ as) :
    card (a :: as) = card as + 1 := by simp [card, h]
                                       -- ðŸŽ‰ no goals

theorem card_le_card_cons (a : Î±) (as : List Î±) : card as â‰¤ card (a :: as) := by
  cases Decidable.em (a âˆˆ as) with
  | inl h => simp [h, Nat.le_refl]
  | inr h => simp [h, Nat.le_succ]

@[simp] theorem card_insert_of_mem {a : Î±} {as : List Î±} (h : a âˆˆ as) :
    card (as.insert a) = card as := by simp [h]
                                       -- ðŸŽ‰ no goals

@[simp] theorem card_insert_of_not_mem {a : Î±} {as : List Î±} (h : a âˆ‰ as) :
    card (as.insert a) = card as + 1 := by simp [h]
                                           -- ðŸŽ‰ no goals

theorem card_remove_of_mem {a : Î±} : âˆ€ {as : List Î±}, a âˆˆ as â†’ card as = card (remove a as) + 1
  | [], h => False.elim (not_mem_nil _ h)
  | (a' :: as), h => by
    cases Decidable.em (a = a') with
    | inl h' =>
      simp [remove, if_pos h']
      cases Decidable.em (a âˆˆ as) with
      | inl h'' =>
        have hâ‚ƒ : a' âˆˆ as := h' â–¸ h''
        simp [card_remove_of_mem h'', hâ‚ƒ]
      | inr h'' =>
        have hâ‚ƒ : a' âˆ‰ as := h' â–¸ h''
        simp [card_cons_of_not_mem hâ‚ƒ, remove_eq_of_not_mem h'']
    | inr h' =>
        have hâ‚ƒ : a âˆˆ as := (mem_cons.1 h).resolve_left h'
        simp [remove, h']
        cases Decidable.em (a' âˆˆ as) with
        | inl h'' =>
          have : a' âˆˆ remove a as := by rw [mem_remove_iff]; exact âŸ¨h'', Ne.symm h'âŸ©
          simp [h'', this, card_remove_of_mem hâ‚ƒ]
        | inr h'' =>
          have : a' âˆ‰ remove a as := fun h â†¦ h'' (mem_of_mem_remove h)
          simp [h'', this, card_remove_of_mem hâ‚ƒ]

theorem card_subset_le : âˆ€ {as bs : List Î±}, as âŠ† bs â†’ card as â‰¤ card bs
  | [], bs, _ => by simp
                    -- ðŸŽ‰ no goals
  | (a :: as), bs, hsub => by
    cases Decidable.em (a âˆˆ as) with
    | inl h' =>
      have hsub' : as âŠ† bs := fun _ xmem â†¦ hsub (mem_cons_of_mem a xmem)
      simp [h', card_subset_le hsub']
    | inr h' =>
      have : a âˆˆ bs := hsub (Mem.head ..)
      rw [card_cons_of_not_mem h', card_remove_of_mem this]
      apply Nat.add_le_add_right
      apply card_subset_le
      intro x xmem
      rw [mem_remove_iff]
      exact âŸ¨hsub (mem_cons_of_mem _ xmem), fun h â†¦ h' (h â–¸ xmem)âŸ©

theorem card_map_le (f : Î± â†’ Î²) (as : List Î±) : card (as.map f) â‰¤ card as := by
  induction as with
  | nil => simp
  | cons a as ih =>
    cases Decidable.em (f a âˆˆ map f as) with
    | inl h =>
      rw [map, card_cons_of_mem h]
      apply Nat.le_trans ih (card_le_card_cons ..)
    | inr h =>
      have : a âˆ‰ as := fun h'' â†¦ h (mem_map_of_mem _ h'')
      rw [map, card_cons_of_not_mem h, card_cons_of_not_mem this]
      exact Nat.add_le_add_right ih _

theorem card_map_eq_of_inj_on {f : Î± â†’ Î²} {as : List Î±} :
    inj_on f as â†’ card (as.map f) = card as := by
  induction as with
  | nil => simp
  | cons a as ih =>
    cases Decidable.em (f a âˆˆ map f as) with
    | inl h =>
      intro inj_on'
      cases (exists_of_mem_map h) with
      | intro x hx =>
        have : x = a := inj_on' (mem_cons_of_mem _ hx.1) (mem_cons_self ..) hx.2
        have h1 : a âˆˆ as := this â–¸ hx.1
        have h2 : inj_on f as := inj_on_of_subset inj_on' (subset_cons _ _)
        rw [map, card_cons_of_mem h, ih h2, card_cons_of_mem h1]
    | inr h =>
      intro inj_on'
      have h1 : a âˆ‰ as := fun h'' â†¦ h (mem_map_of_mem _ h'')
      have h2 : inj_on f as := inj_on_of_subset inj_on' (subset_cons _ _)
      rw [map, card_cons_of_not_mem h, card_cons_of_not_mem h1, ih h2]

theorem card_eq_of_equiv {as bs : List Î±} (h : as.equiv bs) : card as = card bs :=
  let sub_and_sub := equiv_iff_subset_and_subset.1 h
  Nat.le_antisymm (card_subset_le sub_and_sub.1) (card_subset_le sub_and_sub.2)

theorem card_append_disjoint : âˆ€ {as bs : List Î±},
    Disjoint as bs â†’ card (as ++ bs) = card as + card bs
  | [], _, _ => by simp
                   -- ðŸŽ‰ no goals
  | a :: as, bs, disj => by
    have disj' : Disjoint as bs := fun _ h1 h2 â†¦ disj (mem_cons_of_mem a h1) h2
    -- âŠ¢ card (a :: as ++ bs) = card (a :: as) + card bs
    cases Decidable.em (a âˆˆ as) with
    | inl h =>
      simp [h, card_append_disjoint disj']
    | inr h =>
      have h1 : a âˆ‰ bs := fun h' â†¦ disj (mem_cons_self a as) h'
      simp [h, h1, card_append_disjoint disj']
      rw [Nat.add_right_comm]

theorem card_union_disjoint {as bs : List Î±} (h : Disjoint as bs) :
    card (as âˆª bs) = card as + card bs := by
  rw [card_eq_of_equiv (union_equiv_append as bs), card_append_disjoint h]
  -- ðŸŽ‰ no goals

end DecidableEq

end List
