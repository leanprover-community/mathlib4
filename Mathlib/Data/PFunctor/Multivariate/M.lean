/-
Copyright (c) 2018 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Mario Carneiro, Simon Hudon
-/
import Mathlib.Data.PFunctor.Multivariate.Basic
import Mathlib.Data.PFunctor.Univariate.M

#align_import data.pfunctor.multivariate.M from "leanprover-community/mathlib"@"2738d2ca56cbc63be80c3bd48e9ed90ad94e947d"

/-!
# The M construction as a multivariate polynomial functor.

M types are potentially infinite tree-like structures. They are defined
as the greatest fixpoint of a polynomial functor.

## Main definitions

 * `M.mk`     - constructor
 * `M.dest`   - destructor
 * `M.corec`  - corecursor: useful for formulating infinite, productive computations
 * `M.bisim`  - bisimulation: proof technique to show the equality of infinite objects

## Implementation notes

Dual view of M-types:

 * `Mp`: polynomial functor
 * `M`: greatest fixed point of a polynomial functor

Specifically, we define the polynomial functor `Mp` as:

 * A := a possibly infinite tree-like structure without information in the nodes
 * B := given the tree-like structure `t`, `B t` is a valid path
   from the root of `t` to any given node.

As a result `Mp.obj Œ±` is made of a dataless tree and a function from
its valid paths to values of `Œ±`

The difference with the polynomial functor of an initial algebra is
that `A` is a possibly infinite tree.

## Reference

 * Jeremy Avigad, Mario M. Carneiro and Simon Hudon.
   [*Data Types as Quotients of Polynomial Functors*][avigad-carneiro-hudon2019]
-/


universe u

open MvFunctor

namespace MvPFunctor

open TypeVec

variable {n : ‚Ñï} (P : MvPFunctor.{u} (n + 1))

/-- A path from the root of a tree to one of its node -/
inductive M.Path : P.last.M ‚Üí Fin2 n ‚Üí Type u
  | root  (x : P.last.M)
          (a : P.A)
          (f : P.last.B a ‚Üí P.last.M)
          (h : PFunctor.M.dest x = ‚ü®a, f‚ü©)
          (i : Fin2 n)
          (c : P.drop.B a i) : M.Path x i
  | child (x : P.last.M)
          (a : P.A)
          (f : P.last.B a ‚Üí P.last.M)
          (h : PFunctor.M.dest x = ‚ü®a, f‚ü©)
          (j : P.last.B a)
          (i : Fin2 n)
          (c : M.Path (f j) i) : M.Path x i

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.path MvPFunctor.M.Path

instance M.Path.inhabited (x : P.last.M) {i} [Inhabited (P.drop.B x.head i)] :
    Inhabited (M.Path P x i) :=
  let a := PFunctor.M.head x
  let f := PFunctor.M.children x
  ‚ü®M.Path.root _ a f
      (PFunctor.M.casesOn' x
        (r := fun _ => PFunctor.M.dest x = ‚ü®a, f‚ü©)
        <| by
        intros; simp [PFunctor.M.dest_mk, PFunctor.M.children_mk]; rfl)
        -- ‚ä¢ PFunctor.M.dest x = { fst := a, snd := f }
                -- ‚ä¢ PFunctor.M.dest x = { fst := PFunctor.M.head x, snd := PFunctor.M.children x }
                                                                   -- üéâ no goals
      _ default‚ü©

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.path.inhabited MvPFunctor.M.Path.inhabited

/-- Polynomial functor of the M-type of `P`. `A` is a data-less
possibly infinite tree whereas, for a given `a : A`, `B a` is a valid
path in tree `a` so that `mp.Obj Œ±` is made of a tree and a function
from its valid paths to the values it contains -/
def mp : MvPFunctor n where
  A := P.last.M
  B := M.Path P

set_option linter.uppercaseLean3 false in
#align mvpfunctor.Mp MvPFunctor.mp

/-- `n`-ary M-type for `P` -/
def M (Œ± : TypeVec n) : Type _ :=
  P.mp.Obj Œ±

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M MvPFunctor.M

instance mvfunctorM : MvFunctor P.M := by delta M; infer_instance
                                          -- ‚ä¢ MvFunctor fun Œ± => Obj (mp P) Œ±
                                                   -- üéâ no goals

set_option linter.uppercaseLean3 false in
#align mvpfunctor.mvfunctor_M MvPFunctor.mvfunctorM

instance inhabitedM {Œ± : TypeVec _} [I : Inhabited P.A] [‚àÄ i : Fin2 n, Inhabited (Œ± i)] :
    Inhabited (P.M Œ±) :=
  @Obj.inhabited _ (mp P) _ (@PFunctor.M.inhabited P.last I) _

set_option linter.uppercaseLean3 false in
#align mvpfunctor.inhabited_M MvPFunctor.inhabitedM

/-- construct through corecursion the shape of an M-type
without its contents -/
def M.corecShape {Œ≤ : Type u} (g‚ÇÄ : Œ≤ ‚Üí P.A) (g‚ÇÇ : ‚àÄ b : Œ≤, P.last.B (g‚ÇÄ b) ‚Üí Œ≤) :
    Œ≤ ‚Üí P.last.M :=
  PFunctor.M.corec fun b => ‚ü®g‚ÇÄ b, g‚ÇÇ b‚ü©

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.corec_shape MvPFunctor.M.corecShape

/-- Proof of type equality as an arrow -/
def castDropB {a a' : P.A} (h : a = a') : P.drop.B a ‚üπ P.drop.B a' := fun _i b => Eq.recOn h b

set_option linter.uppercaseLean3 false in
#align mvpfunctor.cast_dropB MvPFunctor.castDropB

/-- Proof of type equality as a function -/
def castLastB {a a' : P.A} (h : a = a') : P.last.B a ‚Üí P.last.B a' := fun b => Eq.recOn h b

set_option linter.uppercaseLean3 false in
#align mvpfunctor.cast_lastB MvPFunctor.castLastB

/-- Using corecursion, construct the contents of an M-type -/
def M.corecContents {Œ± : TypeVec.{u} n}
                    {Œ≤ : Type u}
                    (g‚ÇÄ : Œ≤ ‚Üí P.A)
                    (g‚ÇÅ : ‚àÄ b : Œ≤, P.drop.B (g‚ÇÄ b) ‚üπ Œ±)
                    (g‚ÇÇ : ‚àÄ b : Œ≤, P.last.B (g‚ÇÄ b) ‚Üí Œ≤)
                    (x : _)
                    (b : Œ≤)
                    (h: x = M.corecShape P g‚ÇÄ g‚ÇÇ b)
                    : M.Path P x ‚üπ Œ±
  | _, M.Path.root x a f h' i c =>
    have : a = g‚ÇÄ b := by
      rw [h, M.corecShape, PFunctor.M.dest_corec] at h'
      -- ‚ä¢ a = g‚ÇÄ b
      cases h'
      -- ‚ä¢ g‚ÇÄ b = g‚ÇÄ b
      rfl
      -- üéâ no goals
    g‚ÇÅ b i (P.castDropB this i c)
  | _, M.Path.child x a f h' j i c =>
    have h‚ÇÄ : a = g‚ÇÄ b := by
      rw [h, M.corecShape, PFunctor.M.dest_corec] at h'
      -- ‚ä¢ a = g‚ÇÄ b
      cases h'
      -- ‚ä¢ g‚ÇÄ b = g‚ÇÄ b
      rfl
      -- üéâ no goals
    have h‚ÇÅ : f j = M.corecShape P g‚ÇÄ g‚ÇÇ (g‚ÇÇ b (castLastB P h‚ÇÄ j)) := by
      rw [h, M.corecShape, PFunctor.M.dest_corec] at h'
      -- ‚ä¢ f j = corecShape P g‚ÇÄ g‚ÇÇ (g‚ÇÇ b (castLastB P h‚ÇÄ j))
      cases h'
      -- ‚ä¢ ((PFunctor.M.corec fun b => { fst := g‚ÇÄ b, snd := g‚ÇÇ b }) ‚àò g‚ÇÇ b) j = corecS ‚Ä¶
      rfl
      -- üéâ no goals
    M.corecContents g‚ÇÄ g‚ÇÅ g‚ÇÇ (f j) (g‚ÇÇ b (P.castLastB h‚ÇÄ j)) h‚ÇÅ i c

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.corec_contents MvPFunctor.M.corecContents

/-- Corecursor for M-type of `P` -/
def M.corec' {Œ± : TypeVec n} {Œ≤ : Type u} (g‚ÇÄ : Œ≤ ‚Üí P.A) (g‚ÇÅ : ‚àÄ b : Œ≤, P.drop.B (g‚ÇÄ b) ‚üπ Œ±)
    (g‚ÇÇ : ‚àÄ b : Œ≤, P.last.B (g‚ÇÄ b) ‚Üí Œ≤) : Œ≤ ‚Üí P.M Œ± := fun b =>
  ‚ü®M.corecShape P g‚ÇÄ g‚ÇÇ b, M.corecContents P g‚ÇÄ g‚ÇÅ g‚ÇÇ _ _ rfl‚ü©

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.corec' MvPFunctor.M.corec'

/-- Corecursor for M-type of `P` -/
def M.corec {Œ± : TypeVec n} {Œ≤ : Type u} (g : Œ≤ ‚Üí P.Obj (Œ±.append1 Œ≤)) : Œ≤ ‚Üí P.M Œ± :=
  M.corec' P (fun b => (g b).fst) (fun b => dropFun (g b).snd) fun b => lastFun (g b).snd

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.corec MvPFunctor.M.corec

/-- Implementation of destructor for M-type of `P` -/
def M.pathDestLeft {Œ± : TypeVec n} {x : P.last.M} {a : P.A} {f : P.last.B a ‚Üí P.last.M}
    (h : PFunctor.M.dest x = ‚ü®a, f‚ü©) (f' : M.Path P x ‚üπ Œ±) : P.drop.B a ‚üπ Œ± := fun i c =>
  f' i (M.Path.root x a f h i c)

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.path_dest_left MvPFunctor.M.pathDestLeft

/-- Implementation of destructor for M-type of `P` -/
def M.pathDestRight {Œ± : TypeVec n} {x : P.last.M} {a : P.A} {f : P.last.B a ‚Üí P.last.M}
    (h : PFunctor.M.dest x = ‚ü®a, f‚ü©) (f' : M.Path P x ‚üπ Œ±) :
    ‚àÄ j : P.last.B a, M.Path P (f j) ‚üπ Œ± := fun j i c => f' i (M.Path.child x a f h j i c)

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.path_dest_right MvPFunctor.M.pathDestRight

/-- Destructor for M-type of `P` -/
def M.dest' {Œ± : TypeVec n} {x : P.last.M} {a : P.A} {f : P.last.B a ‚Üí P.last.M}
    (h : PFunctor.M.dest x = ‚ü®a, f‚ü©) (f' : M.Path P x ‚üπ Œ±) : P.Obj (Œ±.append1 (P.M Œ±)) :=
  ‚ü®a, splitFun (M.pathDestLeft P h f') fun x => ‚ü®f x, M.pathDestRight P h f' x‚ü©‚ü©

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.dest' MvPFunctor.M.dest'

/-- Destructor for M-types -/
def M.dest {Œ± : TypeVec n} (x : P.M Œ±) : P.Obj (Œ± ::: P.M Œ±) :=
  M.dest' P (Sigma.eta <| PFunctor.M.dest x.fst).symm x.snd

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.dest MvPFunctor.M.dest

/-- Constructor for M-types -/
def M.mk {Œ± : TypeVec n} : P.Obj (Œ±.append1 (P.M Œ±)) ‚Üí P.M Œ± :=
  M.corec _ fun i => appendFun id (M.dest P) <$$> i

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.mk MvPFunctor.M.mk

theorem M.dest'_eq_dest' {Œ± : TypeVec n} {x : P.last.M} {a‚ÇÅ : P.A}
    {f‚ÇÅ : P.last.B a‚ÇÅ ‚Üí P.last.M} (h‚ÇÅ : PFunctor.M.dest x = ‚ü®a‚ÇÅ, f‚ÇÅ‚ü©) {a‚ÇÇ : P.A}
    {f‚ÇÇ : P.last.B a‚ÇÇ ‚Üí P.last.M} (h‚ÇÇ : PFunctor.M.dest x = ‚ü®a‚ÇÇ, f‚ÇÇ‚ü©) (f' : M.Path P x ‚üπ Œ±) :
    M.dest' P h‚ÇÅ f' = M.dest' P h‚ÇÇ f' := by cases h‚ÇÅ.symm.trans h‚ÇÇ; rfl
                                            -- ‚ä¢ dest' P h‚ÇÅ f' = dest' P h‚ÇÇ f'
                                                                    -- üéâ no goals

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.dest'_eq_dest' MvPFunctor.M.dest'_eq_dest'

theorem M.dest_eq_dest' {Œ± : TypeVec n} {x : P.last.M} {a : P.A}
    {f : P.last.B a ‚Üí P.last.M} (h : PFunctor.M.dest x = ‚ü®a, f‚ü©) (f' : M.Path P x ‚üπ Œ±) :
    M.dest P ‚ü®x, f'‚ü© = M.dest' P h f' :=
  M.dest'_eq_dest' _ _ _ _

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.dest_eq_dest' MvPFunctor.M.dest_eq_dest'

theorem M.dest_corec' {Œ± : TypeVec.{u} n} {Œ≤ : Type u} (g‚ÇÄ : Œ≤ ‚Üí P.A)
    (g‚ÇÅ : ‚àÄ b : Œ≤, P.drop.B (g‚ÇÄ b) ‚üπ Œ±) (g‚ÇÇ : ‚àÄ b : Œ≤, P.last.B (g‚ÇÄ b) ‚Üí Œ≤) (x : Œ≤) :
    M.dest P (M.corec' P g‚ÇÄ g‚ÇÅ g‚ÇÇ x) = ‚ü®g‚ÇÄ x, splitFun (g‚ÇÅ x) (M.corec' P g‚ÇÄ g‚ÇÅ g‚ÇÇ ‚àò g‚ÇÇ x)‚ü© :=
  rfl

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.dest_corec' MvPFunctor.M.dest_corec'

theorem M.dest_corec {Œ± : TypeVec n} {Œ≤ : Type u} (g : Œ≤ ‚Üí P.Obj (Œ±.append1 Œ≤)) (x : Œ≤) :
    M.dest P (M.corec P g x) = appendFun id (M.corec P g) <$$> g x := by
  trans
  apply M.dest_corec'
  -- ‚ä¢ { fst := (g x).fst, snd := splitFun (dropFun (g x).snd) ((corec' P (fun b => ‚Ä¶
  cases' g x with a f; dsimp
  -- ‚ä¢ { fst := { fst := a, snd := f }.fst, snd := splitFun (dropFun { fst := a, sn ‚Ä¶
                       -- ‚ä¢ { fst := a, snd := splitFun (dropFun f) ((corec' P (fun b => (g b).fst) (fun ‚Ä¶
  rw [MvPFunctor.map_eq]; congr
  -- ‚ä¢ { fst := a, snd := splitFun (dropFun f) ((corec' P (fun b => (g b).fst) (fun ‚Ä¶
                          -- ‚ä¢ splitFun (dropFun f) ((corec' P (fun b => (g b).fst) (fun b => dropFun (g b) ‚Ä¶
  conv =>
    rhs
    rw [‚Üê split_dropFun_lastFun f, appendFun_comp_splitFun]

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.dest_corec MvPFunctor.M.dest_corec

theorem M.bisim_lemma {Œ± : TypeVec n} {a‚ÇÅ : (mp P).A} {f‚ÇÅ : (mp P).B a‚ÇÅ ‚üπ Œ±} {a' : P.A}
    {f' : (P.B a').drop ‚üπ Œ±} {f‚ÇÅ' : (P.B a').last ‚Üí M P Œ±}
    (e‚ÇÅ : M.dest P ‚ü®a‚ÇÅ, f‚ÇÅ‚ü© = ‚ü®a', splitFun f' f‚ÇÅ'‚ü©) :
    ‚àÉ (g‚ÇÅ' : _)(e‚ÇÅ' : PFunctor.M.dest a‚ÇÅ = ‚ü®a', g‚ÇÅ'‚ü©),
      f' = M.pathDestLeft P e‚ÇÅ' f‚ÇÅ ‚àß
        f‚ÇÅ' = fun x : (last P).B a' => ‚ü®g‚ÇÅ' x, M.pathDestRight P e‚ÇÅ' f‚ÇÅ x‚ü© := by
  generalize ef : @splitFun n _ (append1 Œ± (M P Œ±)) f' f‚ÇÅ' = ff at e‚ÇÅ
  -- ‚ä¢ ‚àÉ g‚ÇÅ' e‚ÇÅ', f' = pathDestLeft P e‚ÇÅ' f‚ÇÅ ‚àß f‚ÇÅ' = fun x => { fst := g‚ÇÅ' x, snd : ‚Ä¶
  let he‚ÇÅ' := PFunctor.M.dest a‚ÇÅ;
  -- ‚ä¢ ‚àÉ g‚ÇÅ' e‚ÇÅ', f' = pathDestLeft P e‚ÇÅ' f‚ÇÅ ‚àß f‚ÇÅ' = fun x => { fst := g‚ÇÅ' x, snd : ‚Ä¶
  rcases e‚ÇÅ' : he‚ÇÅ' with ‚ü®a‚ÇÅ', g‚ÇÅ'‚ü©;
  -- ‚ä¢ ‚àÉ g‚ÇÅ' e‚ÇÅ', f' = pathDestLeft P e‚ÇÅ' f‚ÇÅ ‚àß f‚ÇÅ' = fun x => { fst := g‚ÇÅ' x, snd : ‚Ä¶
  rw [M.dest_eq_dest' _ e‚ÇÅ'] at e‚ÇÅ
  -- ‚ä¢ ‚àÉ g‚ÇÅ' e‚ÇÅ', f' = pathDestLeft P e‚ÇÅ' f‚ÇÅ ‚àß f‚ÇÅ' = fun x => { fst := g‚ÇÅ' x, snd : ‚Ä¶
  cases e‚ÇÅ; exact ‚ü®_, e‚ÇÅ', splitFun_inj ef‚ü©
  -- ‚ä¢ ‚àÉ g‚ÇÅ' e‚ÇÅ', f' = pathDestLeft P e‚ÇÅ' f‚ÇÅ ‚àß f‚ÇÅ' = fun x => { fst := g‚ÇÅ' x, snd : ‚Ä¶
            -- üéâ no goals

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.bisim_lemma MvPFunctor.M.bisim_lemma

theorem M.bisim {Œ± : TypeVec n} (R : P.M Œ± ‚Üí P.M Œ± ‚Üí Prop)
    (h :
      ‚àÄ x y,
        R x y ‚Üí
          ‚àÉ a f f‚ÇÅ f‚ÇÇ,
            M.dest P x = ‚ü®a, splitFun f f‚ÇÅ‚ü© ‚àß
              M.dest P y = ‚ü®a, splitFun f f‚ÇÇ‚ü© ‚àß ‚àÄ i, R (f‚ÇÅ i) (f‚ÇÇ i))
    (x y) (r : R x y) : x = y := by
  cases' x with a‚ÇÅ f‚ÇÅ
  -- ‚ä¢ { fst := a‚ÇÅ, snd := f‚ÇÅ } = y
  cases' y with a‚ÇÇ f‚ÇÇ
  -- ‚ä¢ { fst := a‚ÇÅ, snd := f‚ÇÅ } = { fst := a‚ÇÇ, snd := f‚ÇÇ }
  dsimp [mp] at *
  -- ‚ä¢ { fst := a‚ÇÅ, snd := f‚ÇÅ } = { fst := a‚ÇÇ, snd := f‚ÇÇ }
  have : a‚ÇÅ = a‚ÇÇ := by
    refine'
      PFunctor.M.bisim (fun a‚ÇÅ a‚ÇÇ => ‚àÉ x y, R x y ‚àß x.1 = a‚ÇÅ ‚àß y.1 = a‚ÇÇ) _ _ _
        ‚ü®‚ü®a‚ÇÅ, f‚ÇÅ‚ü©, ‚ü®a‚ÇÇ, f‚ÇÇ‚ü©, r, rfl, rfl‚ü©
    rintro _ _ ‚ü®‚ü®a‚ÇÅ, f‚ÇÅ‚ü©, ‚ü®a‚ÇÇ, f‚ÇÇ‚ü©, r, rfl, rfl‚ü©
    rcases h _ _ r with ‚ü®a', f', f‚ÇÅ', f‚ÇÇ', e‚ÇÅ, e‚ÇÇ, h'‚ü©
    rcases M.bisim_lemma P e‚ÇÅ with ‚ü®g‚ÇÅ', e‚ÇÅ', rfl, rfl‚ü©
    rcases M.bisim_lemma P e‚ÇÇ with ‚ü®g‚ÇÇ', e‚ÇÇ', _, rfl‚ü©
    rw [e‚ÇÅ', e‚ÇÇ']
    exact ‚ü®_, _, _, rfl, rfl, fun b => ‚ü®_, _, h' b, rfl, rfl‚ü©‚ü©
  subst this
  -- ‚ä¢ { fst := a‚ÇÅ, snd := f‚ÇÅ } = { fst := a‚ÇÅ, snd := f‚ÇÇ }
  congr with (i p)
  -- ‚ä¢ f‚ÇÅ i p = f‚ÇÇ i p
  induction' p with x a f h' i c x a f h' i c p IH <;>
  -- ‚ä¢ f‚ÇÅ i (Path.root x a f h' i c) = f‚ÇÇ i (Path.root x a f h' i c)
    try
      rcases h _ _ r with ‚ü®a', f', f‚ÇÅ', f‚ÇÇ', e‚ÇÅ, e‚ÇÇ, h''‚ü©
      rcases M.bisim_lemma P e‚ÇÅ with ‚ü®g‚ÇÅ', e‚ÇÅ', rfl, rfl‚ü©
      rcases M.bisim_lemma P e‚ÇÇ with ‚ü®g‚ÇÇ', e‚ÇÇ', e‚ÇÉ, rfl‚ü©
      cases h'.symm.trans e‚ÇÅ'
      cases h'.symm.trans e‚ÇÇ'
  ¬∑ exact (congr_fun (congr_fun e‚ÇÉ i) c : _)
    -- üéâ no goals
  ¬∑ exact IH _ _ (h'' _)
    -- üéâ no goals

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.bisim MvPFunctor.M.bisim

theorem M.bisim‚ÇÄ {Œ± : TypeVec n} (R : P.M Œ± ‚Üí P.M Œ± ‚Üí Prop) (h‚ÇÄ : Equivalence R)
    (h : ‚àÄ x y, R x y ‚Üí (id ::: Quot.mk R) <$$> M.dest _ x = (id ::: Quot.mk R) <$$> M.dest _ y)
    (x y) (r : R x y) : x = y := by
  apply M.bisim P R _ _ _ r
  -- ‚ä¢ ‚àÄ (x y : M P Œ±), R x y ‚Üí ‚àÉ a f f‚ÇÅ f‚ÇÇ, dest P x = { fst := a, snd := splitFun ‚Ä¶
  clear r x y
  -- ‚ä¢ ‚àÄ (x y : M P Œ±), R x y ‚Üí ‚àÉ a f f‚ÇÅ f‚ÇÇ, dest P x = { fst := a, snd := splitFun ‚Ä¶
  introv Hr
  -- ‚ä¢ ‚àÉ a f f‚ÇÅ f‚ÇÇ, dest P x = { fst := a, snd := splitFun f f‚ÇÅ } ‚àß dest P y = { fs ‚Ä¶
  specialize h _ _ Hr
  -- ‚ä¢ ‚àÉ a f f‚ÇÅ f‚ÇÇ, dest P x = { fst := a, snd := splitFun f f‚ÇÅ } ‚àß dest P y = { fs ‚Ä¶
  clear Hr
  -- ‚ä¢ ‚àÉ a f f‚ÇÅ f‚ÇÇ, dest P x = { fst := a, snd := splitFun f f‚ÇÅ } ‚àß dest P y = { fs ‚Ä¶

  revert h
  -- ‚ä¢ (TypeVec.id ::: Quot.mk R) <$$> dest P x = (TypeVec.id ::: Quot.mk R) <$$> d ‚Ä¶
  rcases M.dest P x with ‚ü®ax, fx‚ü©
  -- ‚ä¢ (TypeVec.id ::: Quot.mk R) <$$> { fst := ax, snd := fx } = (TypeVec.id ::: Q ‚Ä¶
  rcases M.dest P y with ‚ü®ay, fy‚ü©
  -- ‚ä¢ (TypeVec.id ::: Quot.mk R) <$$> { fst := ax, snd := fx } = (TypeVec.id ::: Q ‚Ä¶
  intro h
  -- ‚ä¢ ‚àÉ a f f‚ÇÅ f‚ÇÇ, { fst := ax, snd := fx } = { fst := a, snd := splitFun f f‚ÇÅ } ‚àß ‚Ä¶

  rw [map_eq, map_eq] at h
  -- ‚ä¢ ‚àÉ a f f‚ÇÅ f‚ÇÇ, { fst := ax, snd := fx } = { fst := a, snd := splitFun f f‚ÇÅ } ‚àß ‚Ä¶
  injection h with h‚ÇÄ h‚ÇÅ
  -- ‚ä¢ ‚àÉ a f f‚ÇÅ f‚ÇÇ, { fst := ax, snd := fx } = { fst := a, snd := splitFun f f‚ÇÅ } ‚àß ‚Ä¶
  subst ay
  -- ‚ä¢ ‚àÉ a f f‚ÇÅ f‚ÇÇ, { fst := ax, snd := fx } = { fst := a, snd := splitFun f f‚ÇÅ } ‚àß ‚Ä¶
  simp at h‚ÇÅ
  -- ‚ä¢ ‚àÉ a f f‚ÇÅ f‚ÇÇ, { fst := ax, snd := fx } = { fst := a, snd := splitFun f f‚ÇÅ } ‚àß ‚Ä¶
  have Hdrop : dropFun fx = dropFun fy := by
    replace h‚ÇÅ := congr_arg dropFun h‚ÇÅ
    simpa using h‚ÇÅ
  exists ax, dropFun fx, lastFun fx, lastFun fy
  -- ‚ä¢ { fst := ax, snd := fx } = { fst := ax, snd := splitFun (dropFun fx) (lastFu ‚Ä¶
  rw [split_dropFun_lastFun, Hdrop, split_dropFun_lastFun]
  -- ‚ä¢ { fst := ax, snd := fx } = { fst := ax, snd := fx } ‚àß { fst := ax, snd := fy ‚Ä¶
  simp
  -- ‚ä¢ ‚àÄ (i : TypeVec.last (B P ax)), R (lastFun fx i) (lastFun fy i)
  intro i
  -- ‚ä¢ R (lastFun fx i) (lastFun fy i)
  replace h‚ÇÅ := congr_fun (congr_fun h‚ÇÅ Fin2.fz) i
  -- ‚ä¢ R (lastFun fx i) (lastFun fy i)
  simp [(¬∑ ‚äö ¬∑), appendFun, splitFun] at h‚ÇÅ
  -- ‚ä¢ R (lastFun fx i) (lastFun fy i)
  replace h‚ÇÅ := Quot.exact _ h‚ÇÅ
  -- ‚ä¢ R (lastFun fx i) (lastFun fy i)
  rw [h‚ÇÄ.eqvGen_iff] at h‚ÇÅ
  -- ‚ä¢ R (lastFun fx i) (lastFun fy i)
  exact h‚ÇÅ
  -- üéâ no goals

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.bisim‚ÇÄ MvPFunctor.M.bisim‚ÇÄ

theorem M.bisim' {Œ± : TypeVec n} (R : P.M Œ± ‚Üí P.M Œ± ‚Üí Prop)
    (h : ‚àÄ x y, R x y ‚Üí (id ::: Quot.mk R) <$$> M.dest _ x = (id ::: Quot.mk R) <$$> M.dest _ y)
    (x y) (r : R x y) : x = y := by
  have := M.bisim‚ÇÄ P (EqvGen R) ?_ ?_
  ¬∑ solve_by_elim [EqvGen.rel]
    -- üéâ no goals
  ¬∑ apply EqvGen.is_equivalence
    -- üéâ no goals
  ¬∑ clear r x y
    -- ‚ä¢ ‚àÄ (x y : M P Œ±), EqvGen R x y ‚Üí (TypeVec.id ::: Quot.mk (EqvGen R)) <$$> des ‚Ä¶
    introv Hr
    -- ‚ä¢ (TypeVec.id ::: Quot.mk (EqvGen R)) <$$> dest P x = (TypeVec.id ::: Quot.mk  ‚Ä¶
    have : ‚àÄ x y, R x y ‚Üí EqvGen R x y := @EqvGen.rel _ R
    -- ‚ä¢ (TypeVec.id ::: Quot.mk (EqvGen R)) <$$> dest P x = (TypeVec.id ::: Quot.mk  ‚Ä¶
    induction Hr
    ¬∑ rw [‚Üê Quot.factor_mk_eq R (EqvGen R) this]
      -- ‚ä¢ (TypeVec.id ::: Quot.factor R (EqvGen R) this ‚àò Quot.mk R) <$$> dest P x‚úù =  ‚Ä¶
      rwa [appendFun_comp_id, ‚Üê MvFunctor.map_map, ‚Üê MvFunctor.map_map, h]
      -- üéâ no goals
    -- porting note: `cc` was replaced with `aesop`, maybe there is a more light-weight solution?
    all_goals aesop
    -- üéâ no goals

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.bisim' MvPFunctor.M.bisim'

theorem M.dest_map {Œ± Œ≤ : TypeVec n} (g : Œ± ‚üπ Œ≤) (x : P.M Œ±) :
    M.dest P (g <$$> x) = (appendFun g fun x => g <$$> x) <$$> M.dest P x := by
  cases' x with a f
  -- ‚ä¢ dest P (g <$$> { fst := a, snd := f }) = (g ::: fun x => g <$$> x) <$$> dest ‚Ä¶
  rw [map_eq]
  -- ‚ä¢ dest P { fst := a, snd := g ‚äö f } = (g ::: fun x => g <$$> x) <$$> dest P {  ‚Ä¶
  conv =>
    rhs
    rw [M.dest, M.dest', map_eq, appendFun_comp_splitFun]

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.dest_map MvPFunctor.M.dest_map

theorem M.map_dest {Œ± Œ≤ : TypeVec n} (g : (Œ± ::: P.M Œ±) ‚üπ (Œ≤ ::: P.M Œ≤)) (x : P.M Œ±)
    (h : ‚àÄ x : P.M Œ±, lastFun g x = (dropFun g <$$> x : P.M Œ≤)) :
    g <$$> M.dest P x = M.dest P (dropFun g <$$> x) := by
  rw [M.dest_map]; congr
  -- ‚ä¢ g <$$> dest P x = (dropFun g ::: fun x => dropFun g <$$> x) <$$> dest P x
                   -- ‚ä¢ g = (dropFun g ::: fun x => dropFun g <$$> x)
  apply eq_of_drop_last_eq <;> simp
  -- ‚ä¢ dropFun g = dropFun (dropFun g ::: fun x => dropFun g <$$> x)
                               -- üéâ no goals
                               -- ‚ä¢ lastFun g = fun x => dropFun g <$$> x
  ext1; apply h
  -- ‚ä¢ lastFun g x‚úù = dropFun g <$$> x‚úù
        -- üéâ no goals

set_option linter.uppercaseLean3 false in
#align mvpfunctor.M.map_dest MvPFunctor.M.map_dest

end MvPFunctor
