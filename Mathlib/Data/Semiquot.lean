/-
Copyright (c) 2018 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro
-/
import Mathlib.Data.Set.Lattice

#align_import data.semiquot from "leanprover-community/mathlib"@"09597669f02422ed388036273d8848119699c22f"

/-! # Semiquotients

A data type for semiquotients, which are classically equivalent to
nonempty sets, but are useful for programming; the idea is that
a semiquotient set `S` represents some (particular but unknown)
element of `S`. This can be used to model nondeterministic functions,
which return something in a range of values (represented by the
predicate `S`) but are not completely determined.
-/


/-- A member of `Semiquot Î±` is classically a nonempty `Set Î±`,
  and in the VM is represented by an element of `Î±`; the relation
  between these is that the VM element is required to be a member
  of the set `s`. The specific element of `s` that the VM computes
  is hidden by a quotient construction, allowing for the representation
  of nondeterministic functions. -/
  -- porting notes: removed universe parameter
structure Semiquot (Î± : Type*) where mk' ::
  /-- Set containing some element of `Î±`-/
  s : Set Î±
  /-- Assertion of non-emptiness via `Trunc`-/
  val : Trunc s
#align semiquot Semiquot

namespace Semiquot

variable {Î± : Type*} {Î² : Type*}

instance : Membership Î± (Semiquot Î±) :=
  âŸ¨fun a q => a âˆˆ q.sâŸ©

/-- Construct a `Semiquot Î±` from `h : a âˆˆ s` where `s : Set Î±`. -/
def mk {a : Î±} {s : Set Î±} (h : a âˆˆ s) : Semiquot Î± :=
  âŸ¨s, Trunc.mk âŸ¨a, hâŸ©âŸ©
#align semiquot.mk Semiquot.mk

theorem ext_s {qâ‚ qâ‚‚ : Semiquot Î±} : qâ‚ = qâ‚‚ â†” qâ‚.s = qâ‚‚.s := by
  refine' âŸ¨congr_arg _, fun h => _âŸ©
  -- âŠ¢ qâ‚ = qâ‚‚
  cases' qâ‚ with _ vâ‚; cases' qâ‚‚ with _ vâ‚‚; congr
  -- âŠ¢ { s := sâœ, val := vâ‚ } = qâ‚‚
                       -- âŠ¢ { s := sâœÂ¹, val := vâ‚ } = { s := sâœ, val := vâ‚‚ }
                                            -- âŠ¢ HEq vâ‚ vâ‚‚
  exact Subsingleton.helim (congrArg Trunc (congrArg Set.Elem h)) vâ‚ vâ‚‚
  -- ğŸ‰ no goals
#align semiquot.ext_s Semiquot.ext_s

theorem ext {qâ‚ qâ‚‚ : Semiquot Î±} : qâ‚ = qâ‚‚ â†” âˆ€ a, a âˆˆ qâ‚ â†” a âˆˆ qâ‚‚ :=
  ext_s.trans Set.ext_iff
#align semiquot.ext Semiquot.ext

theorem exists_mem (q : Semiquot Î±) : âˆƒ a, a âˆˆ q :=
  let âŸ¨âŸ¨a, hâŸ©, _âŸ© := q.2.exists_rep
  âŸ¨a, hâŸ©
#align semiquot.exists_mem Semiquot.exists_mem

theorem eq_mk_of_mem {q : Semiquot Î±} {a : Î±} (h : a âˆˆ q) : q = @mk _ a q.1 h :=
  ext_s.2 rfl
#align semiquot.eq_mk_of_mem Semiquot.eq_mk_of_mem

theorem nonempty (q : Semiquot Î±) : q.s.Nonempty :=
  q.exists_mem
#align semiquot.nonempty Semiquot.nonempty

/-- `pure a` is `a` reinterpreted as an unspecified element of `{a}`. -/
protected def pure (a : Î±) : Semiquot Î± :=
  mk (Set.mem_singleton a)
#align semiquot.pure Semiquot.pure

@[simp]
theorem mem_pure' {a b : Î±} : a âˆˆ Semiquot.pure b â†” a = b :=
  Set.mem_singleton_iff
#align semiquot.mem_pure' Semiquot.mem_pure'

/-- Replace `s` in a `Semiquot` with a superset. -/
def blur' (q : Semiquot Î±) {s : Set Î±} (h : q.s âŠ† s) : Semiquot Î± :=
  âŸ¨s, Trunc.lift (fun a : q.s => Trunc.mk âŸ¨a.1, h a.2âŸ©) (fun _ _ => Trunc.eq _ _) q.2âŸ©
#align semiquot.blur' Semiquot.blur'

/-- Replace `s` in a `q : Semiquot Î±` with a union `s âˆª q.s` -/
def blur (s : Set Î±) (q : Semiquot Î±) : Semiquot Î± :=
  blur' q (Set.subset_union_right s q.s)
#align semiquot.blur Semiquot.blur

theorem blur_eq_blur' (q : Semiquot Î±) (s : Set Î±) (h : q.s âŠ† s) : blur s q = blur' q h := by
  unfold blur; congr; exact Set.union_eq_self_of_subset_right h
  -- âŠ¢ blur' q (_ : q.s âŠ† s âˆª q.s) = blur' q h
               -- âŠ¢ s âˆª q.s = s
                      -- ğŸ‰ no goals
#align semiquot.blur_eq_blur' Semiquot.blur_eq_blur'

@[simp]
theorem mem_blur' (q : Semiquot Î±) {s : Set Î±} (h : q.s âŠ† s) {a : Î±} : a âˆˆ blur' q h â†” a âˆˆ s :=
  Iff.rfl
#align semiquot.mem_blur' Semiquot.mem_blur'

/-- Convert a `Trunc Î±` to a `Semiquot Î±`. -/
def ofTrunc (q : Trunc Î±) : Semiquot Î± :=
  âŸ¨Set.univ, q.map fun a => âŸ¨a, trivialâŸ©âŸ©
#align semiquot.of_trunc Semiquot.ofTrunc

/-- Convert a `Semiquot Î±` to a `Trunc Î±`. -/
def toTrunc (q : Semiquot Î±) : Trunc Î± :=
  q.2.map Subtype.val
#align semiquot.to_trunc Semiquot.toTrunc

/- ./././Mathport/Syntax/Translate/Basic.lean:632:2:
warning: expanding binder collection (a b Â«expr âˆˆ Â» q) -/
/-- If `f` is a constant on `q.s`, then `q.liftOn f` is the value of `f`
at any point of `q`. -/
def liftOn (q : Semiquot Î±) (f : Î± â†’ Î²) (h : âˆ€ (a) (_ : a âˆˆ q) (b) (_ : b âˆˆ q), f a = f b) : Î² :=
  Trunc.liftOn q.2 (fun x => f x.1) fun x y => h _ x.2 _ y.2
#align semiquot.lift_on Semiquot.liftOn

/- ./././Mathport/Syntax/Translate/Basic.lean:632:2:
warning: expanding binder collection (a b Â«expr âˆˆ Â» q) -/
theorem liftOn_ofMem (q : Semiquot Î±) (f : Î± â†’ Î²)
    (h : âˆ€ (a) (_ : a âˆˆ q) (b) (_ : b âˆˆ q), f a = f b) (a : Î±) (aq : a âˆˆ q) : liftOn q f h = f a :=
  by revert h; rw [eq_mk_of_mem aq]; intro; rfl
     -- âŠ¢ âˆ€ (h : âˆ€ (a : Î±), a âˆˆ q â†’ âˆ€ (b : Î±), b âˆˆ q â†’ f a = f b), liftOn q f h = f a
               -- âŠ¢ âˆ€ (h : âˆ€ (a_1 : Î±), a_1 âˆˆ mk aq â†’ âˆ€ (b : Î±), b âˆˆ mk aq â†’ f a_1 = f b), liftO â€¦
                                     -- âŠ¢ liftOn (mk aq) f hâœ = f a
                                            -- ğŸ‰ no goals
#align semiquot.lift_on_of_mem Semiquot.liftOn_ofMem

/-- Apply a function to the unknown value stored in a `Semiquot Î±`. -/
def map (f : Î± â†’ Î²) (q : Semiquot Î±) : Semiquot Î² :=
  âŸ¨f '' q.1, q.2.map fun x => âŸ¨f x.1, Set.mem_image_of_mem _ x.2âŸ©âŸ©
#align semiquot.map Semiquot.map

@[simp]
theorem mem_map (f : Î± â†’ Î²) (q : Semiquot Î±) (b : Î²) : b âˆˆ map f q â†” âˆƒ a, a âˆˆ q âˆ§ f a = b :=
  Set.mem_image _ _ _
#align semiquot.mem_map Semiquot.mem_map

/-- Apply a function returning a `Semiquot` to a `Semiquot`. -/
def bind (q : Semiquot Î±) (f : Î± â†’ Semiquot Î²) : Semiquot Î² :=
  âŸ¨â‹ƒ a âˆˆ q.1, (f a).1, q.2.bind fun a => (f a.1).2.map fun b => âŸ¨b.1, Set.mem_biUnion a.2 b.2âŸ©âŸ©
#align semiquot.bind Semiquot.bind

@[simp]
theorem mem_bind (q : Semiquot Î±) (f : Î± â†’ Semiquot Î²) (b : Î²) : b âˆˆ bind q f â†” âˆƒ a âˆˆ q, b âˆˆ f a :=
  by simp_rw [â† exists_prop]; exact Set.mem_iUnionâ‚‚
     -- âŠ¢ b âˆˆ bind q f â†” âˆƒ a _h, b âˆˆ f a
                              -- ğŸ‰ no goals
#align semiquot.mem_bind Semiquot.mem_bind

instance : Monad Semiquot where
  pure := @Semiquot.pure
  map := @Semiquot.map
  bind := @Semiquot.bind

@[simp]
theorem map_def {Î²} : ((Â· <$> Â·) : (Î± â†’ Î²) â†’ Semiquot Î± â†’ Semiquot Î²) = map :=
  rfl
#align semiquot.map_def Semiquot.map_def

@[simp]
theorem bind_def {Î²} : ((Â· >>= Â·) : Semiquot Î± â†’ (Î± â†’ Semiquot Î²) â†’ Semiquot Î²) = bind :=
  rfl
#align semiquot.bind_def Semiquot.bind_def

@[simp]
theorem mem_pure {a b : Î±} : a âˆˆ (pure b : Semiquot Î±) â†” a = b :=
  Set.mem_singleton_iff
#align semiquot.mem_pure Semiquot.mem_pure

theorem mem_pure_self (a : Î±) : a âˆˆ (pure a : Semiquot Î±) :=
  Set.mem_singleton a
#align semiquot.mem_pure_self Semiquot.mem_pure_self

@[simp]
theorem pure_inj {a b : Î±} : (pure a : Semiquot Î±) = pure b â†” a = b :=
  ext_s.trans Set.singleton_eq_singleton_iff
#align semiquot.pure_inj Semiquot.pure_inj

instance : LawfulMonad Semiquot := LawfulMonad.mk'
  (pure_bind := fun {Î± Î²} x f => ext.2 <| by simp)
                                             -- ğŸ‰ no goals
  (bind_assoc := fun {Î± Î²} Î³ s f g =>
    ext.2 <| by
    simp only [bind_def, mem_bind]
    -- âŠ¢ âˆ€ (a : Î³), (âˆƒ a_1, (âˆƒ a, a âˆˆ s âˆ§ a_1 âˆˆ f a) âˆ§ a âˆˆ g a_1) â†” âˆƒ a_1, a_1 âˆˆ s âˆ§  â€¦
    exact fun c => âŸ¨fun âŸ¨b, âŸ¨a, as, bfâŸ©, cgâŸ© => âŸ¨a, as, b, bf, cgâŸ©,
                                      -- ğŸ‰ no goals
      fun âŸ¨a, as, b, bf, cgâŸ© => âŸ¨b, âŸ¨a, as, bfâŸ©, cgâŸ©âŸ©)
  (id_map := fun {Î±} q => ext.2 <| by simp)
  (bind_pure_comp := fun {Î± Î²} f s => ext.2 <| by simp [eq_comm])
                                                  -- ğŸ‰ no goals

instance : LE (Semiquot Î±) :=
  âŸ¨fun s t => s.s âŠ† t.sâŸ©

instance partialOrder : PartialOrder (Semiquot Î±) where
  le s t := âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ x âˆˆ t
  le_refl s := Set.Subset.refl _
  le_trans s t u := Set.Subset.trans
  le_antisymm s t hâ‚ hâ‚‚ := ext_s.2 (Set.Subset.antisymm hâ‚ hâ‚‚)

instance : SemilatticeSup (Semiquot Î±) :=
  { Semiquot.partialOrder with
    sup := fun s => blur s.s
    le_sup_left := fun _ _ => Set.subset_union_left _ _
    le_sup_right := fun _ _ => Set.subset_union_right _ _
    sup_le := fun _ _ _ => Set.union_subset }

@[simp]
theorem pure_le {a : Î±} {s : Semiquot Î±} : pure a â‰¤ s â†” a âˆˆ s :=
  Set.singleton_subset_iff
#align semiquot.pure_le Semiquot.pure_le

/- ./././Mathport/Syntax/Translate/Basic.lean:632:2:
warning: expanding binder collection (a b Â«expr âˆˆ Â» q) -/
/-- Assert that a `Semiquot` contains only one possible value. -/
def IsPure (q : Semiquot Î±) : Prop :=
  âˆ€ (a) (_ : a âˆˆ q) (b) (_ : b âˆˆ q), a = b
#align semiquot.is_pure Semiquot.IsPure

/-- Extract the value from an `IsPure` semiquotient. -/
def get (q : Semiquot Î±) (h : q.IsPure) : Î± :=
  liftOn q id h
#align semiquot.get Semiquot.get

theorem get_mem {q : Semiquot Î±} (p) : get q p âˆˆ q := by
  let âŸ¨a, hâŸ© := exists_mem q
  -- âŠ¢ get q p âˆˆ q
  unfold get; rw [liftOn_ofMem q _ _ a h]; exact h
  -- âŠ¢ liftOn q id p âˆˆ q
              -- âŠ¢ id a âˆˆ q
                                           -- ğŸ‰ no goals
#align semiquot.get_mem Semiquot.get_mem

theorem eq_pure {q : Semiquot Î±} (p) : q = pure (get q p) :=
  ext.2 fun a => by simp; exact âŸ¨fun h => p _ h _ (get_mem _), fun e => e.symm â–¸ get_mem _âŸ©
                    -- âŠ¢ a âˆˆ q â†” a = get q p
                          -- ğŸ‰ no goals
#align semiquot.eq_pure Semiquot.eq_pure

@[simp]
theorem pure_isPure (a : Î±) : IsPure (pure a)
  | b, ab, c, ac => by
    rw [mem_pure] at ab ac
    -- âŠ¢ b = c
    rwa [â†ac] at ab
    -- ğŸ‰ no goals
#align semiquot.pure_is_pure Semiquot.pure_isPure

theorem isPure_iff {s : Semiquot Î±} : IsPure s â†” âˆƒ a, s = pure a :=
  âŸ¨fun h => âŸ¨_, eq_pure hâŸ©, fun âŸ¨_, eâŸ© => e.symm â–¸ pure_isPure _âŸ©
#align semiquot.is_pure_iff Semiquot.isPure_iff

theorem IsPure.mono {s t : Semiquot Î±} (st : s â‰¤ t) (h : IsPure t) : IsPure s
  | _, as, _, bs => h _ (st as) _ (st bs)
#align semiquot.is_pure.mono Semiquot.IsPure.mono

theorem IsPure.min {s t : Semiquot Î±} (h : IsPure t) : s â‰¤ t â†” s = t :=
  âŸ¨fun st =>
    le_antisymm st <| by
      rw [eq_pure h, eq_pure (h.mono st)]; simp; exact h _ (get_mem _) _ (st <| get_mem _),
      -- âŠ¢ pure (get t h) â‰¤ pure (get s (_ : IsPure s))
                                           -- âŠ¢ get t h = get s (_ : IsPure s)
                                                 -- ğŸ‰ no goals
    le_of_eqâŸ©
#align semiquot.is_pure.min Semiquot.IsPure.min

theorem isPure_of_subsingleton [Subsingleton Î±] (q : Semiquot Î±) : IsPure q
  | _, _, _, _ => Subsingleton.elim _ _
#align semiquot.is_pure_of_subsingleton Semiquot.isPure_of_subsingleton

/-- `univ : Semiquot Î±` represents an unspecified element of `univ : Set Î±`. -/
def univ [Inhabited Î±] : Semiquot Î± :=
  mk <| Set.mem_univ default
#align semiquot.univ Semiquot.univ

instance [Inhabited Î±] : Inhabited (Semiquot Î±) :=
  âŸ¨univâŸ©

@[simp]
theorem mem_univ [Inhabited Î±] : âˆ€ a, a âˆˆ @univ Î± _ :=
  @Set.mem_univ Î±
#align semiquot.mem_univ Semiquot.mem_univ

@[congr]
theorem univ_unique (I J : Inhabited Î±) : @univ _ I = @univ _ J :=
  ext.2 <| fun a => refl (a âˆˆ univ)
#align semiquot.univ_unique Semiquot.univ_unique

@[simp]
theorem isPure_univ [Inhabited Î±] : @IsPure Î± univ â†” Subsingleton Î± :=
  âŸ¨fun h => âŸ¨fun a b => h a trivial b trivialâŸ©, fun âŸ¨hâŸ© a _ b _ => h a bâŸ©
#align semiquot.is_pure_univ Semiquot.isPure_univ

instance [Inhabited Î±] : OrderTop (Semiquot Î±) where
  top := univ
  le_top _ := Set.subset_univ _

end Semiquot
