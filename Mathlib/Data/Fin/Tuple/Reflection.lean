/-
Copyright (c) 2022 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.Data.Fin.VecNotation
import Mathlib.Algebra.BigOperators.Fin

#align_import data.fin.tuple.reflection from "leanprover-community/mathlib"@"d95bef0d215ea58c0fd7bbc4b151bf3fe952c095"

/-!
# Lemmas for tuples `Fin m â†’ Î±`

This file contains alternative definitions of common operators on vectors which expand
definitionally to the expected expression when evaluated on `![]` notation.

This allows "proof by reflection", where we prove `f = ![f 0, f 1]` by defining
`FinVec.etaExpand f` to be equal to the RHS definitionally, and then prove that
`f = etaExpand f`.

The definitions in this file should normally not be used directly; the intent is for the
corresponding `*_eq` lemmas to be used in a place where they are definitionally unfolded.

## Main definitions

* `FinVec.seq`
* `FinVec.map`
* `FinVec.sum`
* `FinVec.etaExpand`
-/


namespace FinVec

variable {m n : â„•} {Î± Î² Î³ : Type*}

/-- Evaluate `FinVec.seq f v = ![(f 0) (v 0), (f 1) (v 1), ...]` -/
def seq : âˆ€ {m}, (Fin m â†’ Î± â†’ Î²) â†’ (Fin m â†’ Î±) â†’ Fin m â†’ Î²
  | 0, _, _ => ![]
  | _ + 1, f, v => Matrix.vecCons (f 0 (v 0)) (seq (Matrix.vecTail f) (Matrix.vecTail v))
#align fin_vec.seq FinVec.seq

@[simp]
theorem seq_eq : âˆ€ {m} (f : Fin m â†’ Î± â†’ Î²) (v : Fin m â†’ Î±), seq f v = fun i => f i (v i)
  | 0, f, v => Subsingleton.elim _ _
  | n + 1, f, v =>
    funext fun i => by
      simp_rw [seq, seq_eq]
      -- âŠ¢ Matrix.vecCons (f 0 (v 0)) (fun i => Matrix.vecTail f i (Matrix.vecTail v i) â€¦
      refine' i.cases _ fun i => _
      -- âŠ¢ Matrix.vecCons (f 0 (v 0)) (fun i => Matrix.vecTail f i (Matrix.vecTail v i) â€¦
      Â· rfl
        -- ðŸŽ‰ no goals
      Â· rw [Matrix.cons_val_succ]
        -- âŠ¢ Matrix.vecTail f i (Matrix.vecTail v i) = f (Fin.succ i) (v (Fin.succ i))
        rfl
        -- ðŸŽ‰ no goals
#align fin_vec.seq_eq FinVec.seq_eq

example {fâ‚ fâ‚‚ : Î± â†’ Î²} (aâ‚ aâ‚‚ : Î±) : seq ![fâ‚, fâ‚‚] ![aâ‚, aâ‚‚] = ![fâ‚ aâ‚, fâ‚‚ aâ‚‚] := rfl

/-- `FinVec.map f v = ![f (v 0), f (v 1), ...]` -/
def map (f : Î± â†’ Î²) {m} : (Fin m â†’ Î±) â†’ Fin m â†’ Î² :=
  seq fun _ => f
#align fin_vec.map FinVec.map

/-- This can be use to prove
```lean
example {f : Î± â†’ Î²} (aâ‚ aâ‚‚ : Î±) : f âˆ˜ ![aâ‚, aâ‚‚] = ![f aâ‚, f aâ‚‚] :=
  (map_eq _ _).symm
```
-/
@[simp]
theorem map_eq (f : Î± â†’ Î²) {m} (v : Fin m â†’ Î±) : map f v = f âˆ˜ v :=
  seq_eq _ _
#align fin_vec.map_eq FinVec.map_eq

example {f : Î± â†’ Î²} (aâ‚ aâ‚‚ : Î±) : f âˆ˜ ![aâ‚, aâ‚‚] = ![f aâ‚, f aâ‚‚] :=
  (map_eq _ _).symm

/-- Expand `v` to `![v 0, v 1, ...]` -/
def etaExpand {m} (v : Fin m â†’ Î±) : Fin m â†’ Î± :=
  map id v
#align fin_vec.eta_expand FinVec.etaExpand

/-- This can be use to prove
```lean
example (a : Fin 2 â†’ Î±) : a = ![a 0, a 1] :=
  (etaExpand_eq _).symm
```
-/
@[simp]
theorem etaExpand_eq {m} (v : Fin m â†’ Î±) : etaExpand v = v :=
  map_eq id v
#align fin_vec.eta_expand_eq FinVec.etaExpand_eq

example (a : Fin 2 â†’ Î±) : a = ![a 0, a 1] :=
  (etaExpand_eq _).symm

/-- `âˆ€` with better defeq for `âˆ€ x : Fin m â†’ Î±, P x`. -/
def Forall : âˆ€ {m} (_ : (Fin m â†’ Î±) â†’ Prop), Prop
  | 0, P => P ![]
  | _ + 1, P => âˆ€ x : Î±, Forall fun v => P (Matrix.vecCons x v)
#align fin_vec.forall FinVec.Forall

/-- This can be use to prove
```lean
example (P : (Fin 2 â†’ Î±) â†’ Prop) : (âˆ€ f, P f) â†” âˆ€ aâ‚€ aâ‚, P ![aâ‚€, aâ‚] :=
  (forall_iff _).symm
```
-/
@[simp]
theorem forall_iff : âˆ€ {m} (P : (Fin m â†’ Î±) â†’ Prop), Forall P â†” âˆ€ x, P x
  | 0, P => by
    simp only [Forall, Fin.forall_fin_zero_pi]
    -- âŠ¢ P ![] â†” P finZeroElim
    rfl
    -- ðŸŽ‰ no goals
  | .succ n, P => by simp only [Forall, forall_iff, Fin.forall_fin_succ_pi, Matrix.vecCons]
                     -- ðŸŽ‰ no goals
#align fin_vec.forall_iff FinVec.forall_iff

example (P : (Fin 2 â†’ Î±) â†’ Prop) : (âˆ€ f, P f) â†” âˆ€ aâ‚€ aâ‚, P ![aâ‚€, aâ‚] :=
  (forall_iff _).symm

/-- `âˆƒ` with better defeq for `âˆƒ x : Fin m â†’ Î±, P x`. -/
def Exists : âˆ€ {m} (_ : (Fin m â†’ Î±) â†’ Prop), Prop
  | 0, P => P ![]
  | _ + 1, P => âˆƒ x : Î±, Exists fun v => P (Matrix.vecCons x v)
#align fin_vec.exists FinVec.Exists

/-- This can be use to prove
```lean
example (P : (Fin 2 â†’ Î±) â†’ Prop) : (âˆƒ f, P f) â†” âˆƒ aâ‚€ aâ‚, P ![aâ‚€, aâ‚] :=
  (exists_iff _).symm
```
-/
theorem exists_iff : âˆ€ {m} (P : (Fin m â†’ Î±) â†’ Prop), Exists P â†” âˆƒ x, P x
  | 0, P => by
    simp only [Exists, Fin.exists_fin_zero_pi, Matrix.vecEmpty]
    -- âŠ¢ P Fin.elim0' â†” P finZeroElim
    rfl
    -- ðŸŽ‰ no goals
  | .succ n, P => by simp only [Exists, exists_iff, Fin.exists_fin_succ_pi, Matrix.vecCons]
                     -- ðŸŽ‰ no goals
#align fin_vec.exists_iff FinVec.exists_iff

example (P : (Fin 2 â†’ Î±) â†’ Prop) : (âˆƒ f, P f) â†” âˆƒ aâ‚€ aâ‚, P ![aâ‚€, aâ‚] :=
  (exists_iff _).symm

/-- `Finset.univ.sum` with better defeq for `Fin`. -/
def sum [Add Î±] [Zero Î±] : âˆ€ {m} (_ : Fin m â†’ Î±), Î±
  | 0, _ => 0
  | 1, v => v 0
  -- porting note: inline `âˆ˜` since it is no longer reducible
  | _ + 2, v => sum (fun i => v (Fin.castSucc i)) + v (Fin.last _)
#align fin_vec.sum FinVec.sum

open BigOperators

/-- This can be used to prove
```lean
example [AddCommMonoid Î±] (a : Fin 3 â†’ Î±) : âˆ‘ i, a i = a 0 + a 1 + a 2 :=
  (sum_eq _).symm
```
-/
@[simp]
theorem sum_eq [AddCommMonoid Î±] : âˆ€ {m} (a : Fin m â†’ Î±), sum a = âˆ‘ i, a i
  | 0, a => rfl
  | 1, a => (Fintype.sum_unique a).symm
  | n + 2, a => by rw [Fin.sum_univ_castSucc, sum, sum_eq]
                   -- ðŸŽ‰ no goals
#align fin_vec.sum_eq FinVec.sum_eq

example [AddCommMonoid Î±] (a : Fin 3 â†’ Î±) : âˆ‘ i, a i = a 0 + a 1 + a 2 :=
  (sum_eq _).symm

end FinVec
