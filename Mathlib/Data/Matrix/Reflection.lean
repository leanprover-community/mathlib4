/-
Copyright (c) 2022 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.Data.Matrix.Notation
import Mathlib.Data.Matrix.Basic
import Mathlib.Data.Fin.Tuple.Reflection

#align_import data.matrix.reflection from "leanprover-community/mathlib"@"820b22968a2bc4a47ce5cf1d2f36a9ebe52510aa"

/-!
# Lemmas for concrete matrices `Matrix (Fin m) (Fin n) Î±`

This file contains alternative definitions of common operators on matrices that expand
definitionally to the expected expression when evaluated on `!![]` notation.

This allows "proof by reflection", where we prove `A = !![A 0 0, A 0 1;  A 1 0, A 1 1]` by defining
`Matrix.etaExpand A` to be equal to the RHS definitionally, and then prove that
`A = eta_expand A`.

The definitions in this file should normally not be used directly; the intent is for the
corresponding `*_eq` lemmas to be used in a place where they are definitionally unfolded.

## Main definitions

* `Matrix.transposeáµ£`
* `Matrix.dotProductáµ£`
* `Matrix.muláµ£`
* `Matrix.mulVecáµ£`
* `Matrix.vecMuláµ£`
* `Matrix.etaExpand`

-/


open Matrix

namespace Matrix

variable {l m n : â„•} {Î± Î² : Type*}

/-- `âˆ€` with better defeq for `âˆ€ x : Matrix (Fin m) (Fin n) Î±, P x`. -/
def Forall : âˆ€ {m n} (_ : Matrix (Fin m) (Fin n) Î± â†’ Prop), Prop
  | 0, _, P => P (of ![])
  | _ + 1, _, P => FinVec.Forall fun r => Forall fun A => P (of (Matrix.vecCons r A))
#align matrix.forall Matrix.Forall

/-- This can be use to prove
```lean
example (P : Matrix (Fin 2) (Fin 3) Î± â†’ Prop) :
  (âˆ€ x, P x) â†” âˆ€ a b c d e f, P !![a, b, c; d, e, f] :=
(forall_iff _).symm
```
-/
theorem forall_iff : âˆ€ {m n} (P : Matrix (Fin m) (Fin n) Î± â†’ Prop), Forall P â†” âˆ€ x, P x
  | 0, n, P => Iff.symm Fin.forall_fin_zero_pi
  | m + 1, n, P => by
    simp only [Forall, FinVec.forall_iff, forall_iff]
    -- âŠ¢ (âˆ€ (x : Fin n â†’ Î±) (x_1 : Matrix (Fin (Nat.add m 0)) (Fin n) Î±), P (â†‘of (vec â€¦
    exact Iff.symm Fin.forall_fin_succ_pi
    -- ðŸŽ‰ no goals
#align matrix.forall_iff Matrix.forall_iff

example (P : Matrix (Fin 2) (Fin 3) Î± â†’ Prop) :
    (âˆ€ x, P x) â†” âˆ€ a b c d e f, P !![a, b, c; d, e, f] :=
  (forall_iff _).symm

/-- `âˆƒ` with better defeq for `âˆƒ x : Matrix (Fin m) (Fin n) Î±, P x`. -/
def Exists : âˆ€ {m n} (_ : Matrix (Fin m) (Fin n) Î± â†’ Prop), Prop
  | 0, _, P => P (of ![])
  | _ + 1, _, P => FinVec.Exists fun r => Exists fun A => P (of (Matrix.vecCons r A))
#align matrix.exists Matrix.Exists

/-- This can be use to prove
```lean
example (P : Matrix (Fin 2) (Fin 3) Î± â†’ Prop) :
  (âˆƒ x, P x) â†” âˆƒ a b c d e f, P !![a, b, c; d, e, f] :=
(exists_iff _).symm
```
-/
theorem exists_iff : âˆ€ {m n} (P : Matrix (Fin m) (Fin n) Î± â†’ Prop), Exists P â†” âˆƒ x, P x
  | 0, n, P => Iff.symm Fin.exists_fin_zero_pi
  | m + 1, n, P => by
    simp only [Exists, FinVec.exists_iff, exists_iff]
    -- âŠ¢ (âˆƒ x x_1, P (â†‘of (vecCons x x_1))) â†” âˆƒ x, P x
    exact Iff.symm Fin.exists_fin_succ_pi
    -- ðŸŽ‰ no goals
#align matrix.exists_iff Matrix.exists_iff

example (P : Matrix (Fin 2) (Fin 3) Î± â†’ Prop) :
    (âˆƒ x, P x) â†” âˆƒ a b c d e f, P !![a, b, c; d, e, f] :=
  (exists_iff _).symm

/-- `Matrix.transpose` with better defeq for `Fin` -/
def transposeáµ£ : âˆ€ {m n}, Matrix (Fin m) (Fin n) Î± â†’ Matrix (Fin n) (Fin m) Î±
  | _, 0, _ => of ![]
  | _, _ + 1, A =>
    of <| vecCons (FinVec.map (fun v : Fin _ â†’ Î± => v 0) A) (transposeáµ£ (A.submatrix id Fin.succ))
#align matrix.transposeáµ£ Matrix.transposeáµ£

/-- This can be used to prove
```lean
example (a b c d : Î±) : transpose !![a, b; c, d] = !![a, c; b, d] := (transposeáµ£_eq _).symm
```
-/
@[simp]
theorem transposeáµ£_eq : âˆ€ {m n} (A : Matrix (Fin m) (Fin n) Î±), transposeáµ£ A = transpose A
  | _, 0, A => Subsingleton.elim _ _
  | m, n + 1, A =>
    Matrix.ext fun i j => by
      simp_rw [transposeáµ£, transposeáµ£_eq]
      -- âŠ¢ â†‘of (vecCons (FinVec.map (fun v => v 0) A) (submatrix A id Fin.succ)áµ€) i j = â€¦
      refine' i.cases _ fun i => _
      -- âŠ¢ â†‘of (vecCons (FinVec.map (fun v => v 0) A) (submatrix A id Fin.succ)áµ€) 0 j = â€¦
      Â· dsimp
        -- âŠ¢ FinVec.map (fun v => v 0) A j = A j 0
        rw [FinVec.map_eq, Function.comp_apply]
        -- ðŸŽ‰ no goals
      Â· simp only [of_apply, Matrix.cons_val_succ]
        -- âŠ¢ (submatrix A id Fin.succ)áµ€ i j = Aáµ€ (Fin.succ i) j
        rfl
        -- ðŸŽ‰ no goals
#align matrix.transposeáµ£_eq Matrix.transposeáµ£_eq

example (a b c d : Î±) : transpose !![a, b; c, d] = !![a, c; b, d] :=
  (transposeáµ£_eq _).symm

/-- `Matrix.dotProduct` with better defeq for `Fin` -/
def dotProductáµ£ [Mul Î±] [Add Î±] [Zero Î±] {m} (a b : Fin m â†’ Î±) : Î± :=
  FinVec.sum <| FinVec.seq (FinVec.map (Â· * Â·) a) b
#align matrix.dot_productáµ£ Matrix.dotProductáµ£

/-- This can be used to prove
```lean
example (a b c d : Î±) [Mul Î±] [AddCommMonoid Î±] :
  dot_product ![a, b] ![c, d] = a * c + b * d :=
(dot_productáµ£_eq _ _).symm
```
-/
@[simp]
theorem dotProductáµ£_eq [Mul Î±] [AddCommMonoid Î±] {m} (a b : Fin m â†’ Î±) :
    dotProductáµ£ a b = dotProduct a b := by
  simp_rw [dotProductáµ£, dotProduct, FinVec.sum_eq, FinVec.seq_eq, FinVec.map_eq,
      Function.comp_apply]
#align matrix.dot_productáµ£_eq Matrix.dotProductáµ£_eq

example (a b c d : Î±) [Mul Î±] [AddCommMonoid Î±] : dotProduct ![a, b] ![c, d] = a * c + b * d :=
  (dotProductáµ£_eq _ _).symm

/-- `Matrix.mul` with better defeq for `Fin` -/
def muláµ£ [Mul Î±] [Add Î±] [Zero Î±] (A : Matrix (Fin l) (Fin m) Î±) (B : Matrix (Fin m) (Fin n) Î±) :
    Matrix (Fin l) (Fin n) Î± :=
  of <| FinVec.map (fun vâ‚ => FinVec.map (fun vâ‚‚ => dotProductáµ£ vâ‚ vâ‚‚) Báµ€) A
#align matrix.muláµ£ Matrix.muláµ£

/-- This can be used to prove
```lean
example [AddCommMonoid Î±] [Mul Î±] (aâ‚â‚ aâ‚â‚‚ aâ‚‚â‚ aâ‚‚â‚‚ bâ‚â‚ bâ‚â‚‚ bâ‚‚â‚ bâ‚‚â‚‚ : Î±) :
  !![aâ‚â‚, aâ‚â‚‚;
     aâ‚‚â‚, aâ‚‚â‚‚] * !![bâ‚â‚, bâ‚â‚‚;
                    bâ‚‚â‚, bâ‚‚â‚‚] =
  !![aâ‚â‚*bâ‚â‚ + aâ‚â‚‚*bâ‚‚â‚, aâ‚â‚*bâ‚â‚‚ + aâ‚â‚‚*bâ‚‚â‚‚;
     aâ‚‚â‚*bâ‚â‚ + aâ‚‚â‚‚*bâ‚‚â‚, aâ‚‚â‚*bâ‚â‚‚ + aâ‚‚â‚‚*bâ‚‚â‚‚] :=
(muláµ£_eq _ _).symm
```
-/
@[simp]
theorem muláµ£_eq [Mul Î±] [AddCommMonoid Î±] (A : Matrix (Fin l) (Fin m) Î±)
    (B : Matrix (Fin m) (Fin n) Î±) : muláµ£ A B = A * B := by
  simp [muláµ£, Function.comp, Matrix.transpose]
  -- âŠ¢ (â†‘of fun x x_1 => A x â¬áµ¥ â†‘of (fun x y => B y x) x_1) = A * B
  rfl
  -- ðŸŽ‰ no goals
#align matrix.muláµ£_eq Matrix.muláµ£_eq

example [AddCommMonoid Î±] [Mul Î±] (aâ‚â‚ aâ‚â‚‚ aâ‚‚â‚ aâ‚‚â‚‚ bâ‚â‚ bâ‚â‚‚ bâ‚‚â‚ bâ‚‚â‚‚ : Î±) :
    !![aâ‚â‚, aâ‚â‚‚; aâ‚‚â‚, aâ‚‚â‚‚] * !![bâ‚â‚, bâ‚â‚‚; bâ‚‚â‚, bâ‚‚â‚‚] =
      !![aâ‚â‚ * bâ‚â‚ + aâ‚â‚‚ * bâ‚‚â‚, aâ‚â‚ * bâ‚â‚‚ + aâ‚â‚‚ * bâ‚‚â‚‚;
        aâ‚‚â‚ * bâ‚â‚ + aâ‚‚â‚‚ * bâ‚‚â‚, aâ‚‚â‚ * bâ‚â‚‚ + aâ‚‚â‚‚ * bâ‚‚â‚‚] :=
  (muláµ£_eq _ _).symm

/-- `Matrix.mulVec` with better defeq for `Fin` -/
def mulVecáµ£ [Mul Î±] [Add Î±] [Zero Î±] (A : Matrix (Fin l) (Fin m) Î±) (v : Fin m â†’ Î±) : Fin l â†’ Î± :=
  FinVec.map (fun a => dotProductáµ£ a v) A
#align matrix.mul_vecáµ£ Matrix.mulVecáµ£

/-- This can be used to prove
```lean
example [NonUnitalNonAssocSemiring Î±] (aâ‚â‚ aâ‚â‚‚ aâ‚‚â‚ aâ‚‚â‚‚ bâ‚ bâ‚‚ : Î±) :
  !![aâ‚â‚, aâ‚â‚‚;
     aâ‚‚â‚, aâ‚‚â‚‚].mulVec ![bâ‚, bâ‚‚] = ![aâ‚â‚*bâ‚ + aâ‚â‚‚*bâ‚‚, aâ‚‚â‚*bâ‚ + aâ‚‚â‚‚*bâ‚‚] :=
(mulVecáµ£_eq _ _).symm
```
-/
@[simp]
theorem mulVecáµ£_eq [NonUnitalNonAssocSemiring Î±] (A : Matrix (Fin l) (Fin m) Î±) (v : Fin m â†’ Î±) :
    mulVecáµ£ A v = A.mulVec v := by
  simp [mulVecáµ£, Function.comp]
  -- âŠ¢ (fun x => A x â¬áµ¥ v) = mulVec A v
  rfl
  -- ðŸŽ‰ no goals
#align matrix.mul_vecáµ£_eq Matrix.mulVecáµ£_eq

example [NonUnitalNonAssocSemiring Î±] (aâ‚â‚ aâ‚â‚‚ aâ‚‚â‚ aâ‚‚â‚‚ bâ‚ bâ‚‚ : Î±) :
    !![aâ‚â‚, aâ‚â‚‚; aâ‚‚â‚, aâ‚‚â‚‚].mulVec ![bâ‚, bâ‚‚] = ![aâ‚â‚ * bâ‚ + aâ‚â‚‚ * bâ‚‚, aâ‚‚â‚ * bâ‚ + aâ‚‚â‚‚ * bâ‚‚] :=
  (mulVecáµ£_eq _ _).symm

/-- `Matrix.vecMul` with better defeq for `Fin` -/
def vecMuláµ£ [Mul Î±] [Add Î±] [Zero Î±] (v : Fin l â†’ Î±) (A : Matrix (Fin l) (Fin m) Î±) : Fin m â†’ Î± :=
  FinVec.map (fun a => dotProductáµ£ v a) Aáµ€
#align matrix.vec_muláµ£ Matrix.vecMuláµ£

/-- This can be used to prove
```lean
example [NonUnitalNonAssocSemiring Î±] (aâ‚â‚ aâ‚â‚‚ aâ‚‚â‚ aâ‚‚â‚‚ bâ‚ bâ‚‚ : Î±) :
  vecMul ![bâ‚, bâ‚‚] !![aâ‚â‚, aâ‚â‚‚;
                       aâ‚‚â‚, aâ‚‚â‚‚] = ![bâ‚*aâ‚â‚ + bâ‚‚*aâ‚‚â‚, bâ‚*aâ‚â‚‚ + bâ‚‚*aâ‚‚â‚‚] :=
(vecMuláµ£_eq _ _).symm
```
-/
@[simp]
theorem vecMuláµ£_eq [NonUnitalNonAssocSemiring Î±] (v : Fin l â†’ Î±) (A : Matrix (Fin l) (Fin m) Î±) :
    vecMuláµ£ v A = vecMul v A := by
  simp [vecMuláµ£, Function.comp]
  -- âŠ¢ (fun x => v â¬áµ¥ Aáµ€ x) = vecMul v A
  rfl
  -- ðŸŽ‰ no goals
#align matrix.vec_muláµ£_eq Matrix.vecMuláµ£_eq

example [NonUnitalNonAssocSemiring Î±] (aâ‚â‚ aâ‚â‚‚ aâ‚‚â‚ aâ‚‚â‚‚ bâ‚ bâ‚‚ : Î±) :
    vecMul ![bâ‚, bâ‚‚] !![aâ‚â‚, aâ‚â‚‚; aâ‚‚â‚, aâ‚‚â‚‚] = ![bâ‚ * aâ‚â‚ + bâ‚‚ * aâ‚‚â‚, bâ‚ * aâ‚â‚‚ + bâ‚‚ * aâ‚‚â‚‚] :=
  (vecMuláµ£_eq _ _).symm

/-- Expand `A` to `!![A 0 0, ...; ..., A m n]` -/
def etaExpand {m n} (A : Matrix (Fin m) (Fin n) Î±) : Matrix (Fin m) (Fin n) Î± :=
  Matrix.of (FinVec.etaExpand fun i => FinVec.etaExpand fun j => A i j)
#align matrix.eta_expand Matrix.etaExpand

/-- This can be used to prove
```lean
example (A : Matrix (Fin 2) (Fin 2) Î±) :
  A = !![A 0 0, A 0 1;
         A 1 0, A 1 1] :=
(etaExpand_eq _).symm
```
-/
theorem etaExpand_eq {m n} (A : Matrix (Fin m) (Fin n) Î±) : etaExpand A = A := by
  simp_rw [etaExpand, FinVec.etaExpand_eq, Matrix.of, Equiv.refl_apply]
  -- ðŸŽ‰ no goals
#align matrix.eta_expand_eq Matrix.etaExpand_eq

example (A : Matrix (Fin 2) (Fin 2) Î±) : A = !![A 0 0, A 0 1; A 1 0, A 1 1] :=
  (etaExpand_eq _).symm

end Matrix
