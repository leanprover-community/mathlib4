/-
Copyright (c) 2021 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Eric Wieser
-/
import Mathlib.LinearAlgebra.FreeModule.Finite.Rank
import Mathlib.LinearAlgebra.Matrix.ToLin
import Mathlib.LinearAlgebra.FiniteDimensional
import Mathlib.LinearAlgebra.Matrix.DotProduct
import Mathlib.LinearAlgebra.Determinant
import Mathlib.Data.Complex.Module
import Mathlib.LinearAlgebra.Matrix.DotProductStarClass
import Mathlib.LinearAlgebra.StdBasis
import Mathlib.LinearAlgebra.Dual
import Mathlib.LinearAlgebra.Matrix.Dual

#align_import data.matrix.rank from "leanprover-community/mathlib"@"17219820a8aa8abe85adf5dfde19af1dd1bd8ae7"

/-!
# Rank of matrices

The rank of a matrix `A` is defined to be the rank of range of the linear map corresponding to `A`.
This definition does not depend on the choice of basis, see `Matrix.rank_eq_finrank_range_toLin`.

## Main declarations

* `Matrix.rank`: the rank of a matrix

## TODO

* Do a better job of generalizing over `‚Ñö`, `‚Ñù`, and `‚ÑÇ` in `Matrix.rank_transpose` and
  `Matrix.rank_conjTranspose`. See
  [this Zulip thread](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/row.20rank.20equals.20column.20rank/near/350462992).

-/


open Matrix

namespace Matrix

open FiniteDimensional

variable {l m n o R : Type _} [m_fin : Fintype m] [Fintype n] [Fintype o]

section CommRing

variable [CommRing R]

/-- The rank of a matrix is the rank of its image. -/
noncomputable def rank (A : Matrix m n R) : ‚Ñï :=
  finrank R <| LinearMap.range A.mulVecLin
#align matrix.rank Matrix.rank

@[simp]
theorem rank_one [StrongRankCondition R] [DecidableEq n] :
    rank (1 : Matrix n n R) = Fintype.card n := by
  rw [rank, mulVecLin_one, LinearMap.range_id, finrank_top, finrank_pi]
#align matrix.rank_one Matrix.rank_one

@[simp]
theorem rank_zero [Nontrivial R] : rank (0 : Matrix m n R) = 0 := by
  rw [rank, mulVecLin_zero, LinearMap.range_zero, finrank_bot]
#align matrix.rank_zero Matrix.rank_zero

theorem rank_le_card_width [StrongRankCondition R] (A : Matrix m n R) :
    A.rank ‚â§ Fintype.card n := by
  haveI : Module.Finite R (n ‚Üí R) := Module.Finite.pi
  haveI : Module.Free R (n ‚Üí R) := Module.Free.pi _ _
  exact A.mulVecLin.finrank_range_le.trans_eq (finrank_pi _)
#align matrix.rank_le_card_width Matrix.rank_le_card_width

theorem rank_le_width [StrongRankCondition R] {m n : ‚Ñï} (A : Matrix (Fin m) (Fin n) R) :
    A.rank ‚â§ n :=
  A.rank_le_card_width.trans <| (Fintype.card_fin n).le
#align matrix.rank_le_width Matrix.rank_le_width

theorem rank_mul_le_left [StrongRankCondition R] (A : Matrix m n R) (B : Matrix n o R) :
    (A ‚¨ù B).rank ‚â§ A.rank := by
  rw [rank, rank, mulVecLin_mul]
  exact Cardinal.toNat_le_of_le_of_lt_aleph0 (rank_lt_aleph0 _ _) (LinearMap.rank_comp_le_left _ _)
#align matrix.rank_mul_le_left Matrix.rank_mul_le_left

theorem rank_mul_le_right [StrongRankCondition R] (A : Matrix l m R) (B : Matrix m n R) :
    (A ‚¨ù B).rank ‚â§ B.rank := by
  rw [rank, rank, mulVecLin_mul]
  exact
    finrank_le_finrank_of_rank_le_rank (LinearMap.lift_rank_comp_le_right _ _) (rank_lt_aleph0 _ _)
#align matrix.rank_mul_le_right Matrix.rank_mul_le_right

theorem rank_mul_le [StrongRankCondition R] (A : Matrix m n R) (B : Matrix n o R) :
    (A ‚¨ù B).rank ‚â§ min A.rank B.rank :=
  le_min (rank_mul_le_left _ _) (rank_mul_le_right _ _)
#align matrix.rank_mul_le Matrix.rank_mul_le

theorem rank_unit [StrongRankCondition R] [DecidableEq n] (A : (Matrix n n R)À£) :
    (A : Matrix n n R).rank = Fintype.card n := by
  refine' le_antisymm (rank_le_card_width A) _
  have := rank_mul_le_left (A : Matrix n n R) (‚ÜëA‚Åª¬π : Matrix n n R)
  rwa [‚Üê mul_eq_mul, ‚Üê Units.val_mul, mul_inv_self, Units.val_one, rank_one] at this
#align matrix.rank_unit Matrix.rank_unit

theorem rank_of_isUnit [StrongRankCondition R] [DecidableEq n] (A : Matrix n n R) (h : IsUnit A) :
    A.rank = Fintype.card n := by
  obtain ‚ü®A, rfl‚ü© := h
  exact rank_unit A
#align matrix.rank_of_is_unit Matrix.rank_of_isUnit

/-- Right multiplying by an invertible matrix does not change the rank -/
lemma rank_mul_eq_left_of_isUnit_det [DecidableEq n]
    (A : Matrix n n R) (B : Matrix m n R) (hA : IsUnit A.det) :
    (B ‚¨ù A).rank = B.rank := by
  suffices : Function.Surjective A.mulVecLin
  ¬∑ rw [rank, mulVecLin_mul, LinearMap.range_comp_of_range_eq_top _
    (LinearMap.range_eq_top.mpr this), ‚Üê rank]
  intro v
  exact ‚ü®(A‚Åª¬π).mulVecLin v, by simp [mul_nonsing_inv _ hA]‚ü©

/-- Left multiplying by an invertible matrix does not change the rank -/
lemma rank_mul_eq_right_of_isUnit_det [DecidableEq m]
    (A : Matrix m m R) (B : Matrix m n R) (hA : IsUnit A.det) :
    (A ‚¨ù B).rank = B.rank := by
  let b : Basis m R (m ‚Üí R) := Pi.basisFun R m
  replace hA : IsUnit (LinearMap.toMatrix b b A.mulVecLin).det := by
    convert hA; rw [‚Üê LinearEquiv.eq_symm_apply]; rfl
  have hAB : mulVecLin (A ‚¨ù B) = (LinearEquiv.ofIsUnitDet hA).comp (mulVecLin B) := by ext; simp
  rw [rank, rank, hAB, LinearMap.range_comp, LinearEquiv.finrank_map_eq]

/-- Taking a subset of the rows and permuting the columns reduces the rank. -/
theorem rank_submatrix_le [StrongRankCondition R] [Fintype m] (f : n ‚Üí m) (e : n ‚âÉ m)
    (A : Matrix m m R) : rank (A.submatrix f e) ‚â§ rank A := by
  rw [rank, rank, mulVecLin_submatrix, LinearMap.range_comp, LinearMap.range_comp,
    show LinearMap.funLeft R R e.symm = LinearEquiv.funCongrLeft R R e.symm from rfl,
    LinearEquiv.range, Submodule.map_top]
  exact Submodule.finrank_map_le _ _
#align matrix.rank_submatrix_le Matrix.rank_submatrix_le

theorem rank_reindex [Fintype m] (e‚ÇÅ e‚ÇÇ : m ‚âÉ n) (A : Matrix m m R) :
    rank (reindex e‚ÇÅ e‚ÇÇ A) = rank A := by
  rw [rank, rank, mulVecLin_reindex, LinearMap.range_comp, LinearMap.range_comp,
    LinearEquiv.range, Submodule.map_top, LinearEquiv.finrank_map_eq]
#align matrix.rank_reindex Matrix.rank_reindex

@[simp]
theorem rank_submatrix [Fintype m] (A : Matrix m m R) (e‚ÇÅ e‚ÇÇ : n ‚âÉ m) :
    rank (A.submatrix e‚ÇÅ e‚ÇÇ) = rank A := by
  simpa only [reindex_apply] using rank_reindex e‚ÇÅ.symm e‚ÇÇ.symm A
#align matrix.rank_submatrix Matrix.rank_submatrix

theorem rank_eq_finrank_range_toLin [DecidableEq n] {M‚ÇÅ M‚ÇÇ : Type _} [AddCommGroup M‚ÇÅ]
    [AddCommGroup M‚ÇÇ] [Module R M‚ÇÅ] [Module R M‚ÇÇ] (A : Matrix m n R) (v‚ÇÅ : Basis m R M‚ÇÅ)
    (v‚ÇÇ : Basis n R M‚ÇÇ) : A.rank = finrank R (LinearMap.range (toLin v‚ÇÇ v‚ÇÅ A)) := by
  let e‚ÇÅ := (Pi.basisFun R m).equiv v‚ÇÅ (Equiv.refl _)
  let e‚ÇÇ := (Pi.basisFun R n).equiv v‚ÇÇ (Equiv.refl _)
  have range_e‚ÇÇ : LinearMap.range e‚ÇÇ = ‚ä§ := by
    rw [LinearMap.range_eq_top]
    exact e‚ÇÇ.surjective
  refine' LinearEquiv.finrank_eq (e‚ÇÅ.ofSubmodules _ _ _)
  rw [‚Üê LinearMap.range_comp, ‚Üê LinearMap.range_comp_of_range_eq_top (toLin v‚ÇÇ v‚ÇÅ A) range_e‚ÇÇ]
  congr 1
  apply LinearMap.pi_ext'
  rintro i
  apply LinearMap.ext_ring
  have aux‚ÇÅ := toLin_self (Pi.basisFun R n) (Pi.basisFun R m) A i
  have aux‚ÇÇ := Basis.equiv_apply (Pi.basisFun R n) i v‚ÇÇ
  rw [toLin_eq_toLin', toLin'_apply'] at aux‚ÇÅ
  rw [Pi.basisFun_apply, LinearMap.coe_stdBasis] at aux‚ÇÅ aux‚ÇÇ
  simp only [LinearMap.comp_apply, LinearEquiv.coe_coe, Equiv.refl_apply, aux‚ÇÅ, aux‚ÇÇ,
    LinearMap.coe_single, toLin_self, LinearEquiv.map_sum, LinearEquiv.map_smul, Basis.equiv_apply]
#align matrix.rank_eq_finrank_range_to_lin Matrix.rank_eq_finrank_range_toLin

theorem rank_le_card_height [StrongRankCondition R] (A : Matrix m n R) :
    A.rank ‚â§ Fintype.card m := by
  haveI : Module.Finite R (m ‚Üí R) := Module.Finite.pi
  haveI : Module.Free R (m ‚Üí R) := Module.Free.pi _ _
  exact (Submodule.finrank_le _).trans (finrank_pi R).le
#align matrix.rank_le_card_height Matrix.rank_le_card_height

theorem rank_le_height [StrongRankCondition R] {m n : ‚Ñï} (A : Matrix (Fin m) (Fin n) R) :
    A.rank ‚â§ m :=
  A.rank_le_card_height.trans <| (Fintype.card_fin m).le
#align matrix.rank_le_height Matrix.rank_le_height

/-- The rank of a matrix is the rank of the space spanned by its columns. -/
theorem rank_eq_finrank_span_cols (A : Matrix m n R) :
    A.rank = finrank R (Submodule.span R (Set.range A·µÄ)) := by rw [rank, Matrix.range_mulVecLin]
#align matrix.rank_eq_finrank_span_cols Matrix.rank_eq_finrank_span_cols

end CommRing

section Field
-- Praneeth Kolichala's proof for rank_tranpose see:
-- https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/row.20rank.20equals.20column.20rank/near/360941958

open LinearMap

theorem matrix.rank_transpose' {ùïú i j : Type _}
    [Field ùïú] [Fintype i] [Fintype j] [DecidableEq i][DecidableEq j] (m : Matrix i j ùïú) :
    m.transpose.rank = m.rank := by
  rw [Matrix.rank_eq_finrank_range_toLin m·µÄ (Pi.basisFun ùïú j).dualBasis (Pi.basisFun ùïú i).dualBasis,
  Matrix.toLin_transpose, ‚Üê dualMap_def, finrank_range_dualMap_eq_finrank_range,
  Matrix.toLin_eq_toLin', Matrix.toLin'_apply', Matrix.rank]

end Field

section DotProductInnerProductSpace

variable {m : Type _} [Fintype m]
variable {R : Type _} [Field R] [StarRing R]
variable [DotProductInnerProductSpace m R] [DotProductInnerProductSpace n R]

open FiniteDimensional

theorem ker_mulVecLin_conjTranspose_mul_self' (A : Matrix m n R) :
    LinearMap.ker (A·¥¥ ‚¨ù A).mulVecLin = LinearMap.ker (mulVecLin A) := by
  ext x
  simp only [LinearMap.mem_ker, mulVecLin_apply, ‚Üê mulVec_mulVec]
  constructor
  ¬∑ intro h
    replace h := congr_arg (dotProduct (star x)) h
    haveI : NoZeroDivisors R := inferInstance
    rwa [dotProduct_mulVec, dotProduct_zero, vecMul_conjTranspose, star_star,
      DotProductInnerProductSpace.dotProduct_star_self_eq_zero_iff] at h
  ¬∑ intro h
    rw [h, mulVec_zero]

@[simp]
theorem rank_conjTranspose_mul_self' (A : Matrix m n R) :
    (A·¥¥ ‚¨ù A).rank = A.rank := by
  dsimp only [Matrix.rank]
  refine' add_left_injective (finrank R (LinearMap.ker (mulVecLin A))) _
  dsimp only
  trans finrank R { x // x ‚àà LinearMap.range (mulVecLin (A·¥¥ ‚¨ù A)) } +
    finrank R { x // x ‚àà LinearMap.ker (mulVecLin (A·¥¥ ‚¨ù A)) }
  ¬∑ rw [ker_mulVecLin_conjTranspose_mul_self']
  ¬∑ simp only [LinearMap.finrank_range_add_finrank_ker]

@[simp]
theorem rank_conjTranspose' (A : Matrix m n R) : A·¥¥.rank = A.rank :=
  le_antisymm
    (((rank_conjTranspose_mul_self' _).symm.trans_le <| rank_mul_le_left _ _).trans_eq <|
      congr_arg _ <| conjTranspose_conjTranspose _)
    ((rank_conjTranspose_mul_self' _).symm.trans_le <| rank_mul_le_left _ _)

@[simp]
theorem rank_self_mul_conjTranspose' (A : Matrix m n R) : (A ‚¨ù A·¥¥).rank = A.rank := by
  simpa only [rank_conjTranspose', conjTranspose_conjTranspose] using
    rank_conjTranspose_mul_self' A·¥¥

end DotProductInnerProductSpace

/-! ### Lemmas about transpose and conjugate transpose

This section contains lemmas about the rank of `Matrix.transpose` and `Matrix.conjTranspose`.

Unfortunately the proofs are essentially duplicated between the two; `‚Ñö` is a linearly-ordered ring
but can't be a star-ordered ring, while `‚ÑÇ` is star-ordered (with `open ComplexOrder`) but
not linearly ordered. For now we don't prove the transpose case for `‚ÑÇ`.

TODO: the lemmas `Matrix.rank_transpose` and `Matrix.rank_conjTranspose` current follow a short
proof that is a simple consequence of `Matrix.rank_transpose_mul_self` and
`Matrix.rank_conjTranspose_mul_self`. This proof pulls in unnecessary assumptions on `R`, and should
be replaced with a proof that uses Gaussian reduction or argues via linear combinations.
-/

section LinearOrderedField

variable [Fintype m] [LinearOrderedField R]

theorem ker_mulVecLin_transpose_mul_self (A : Matrix m n R) :
    LinearMap.ker (A·µÄ ‚¨ù A).mulVecLin = LinearMap.ker (mulVecLin A) := by
  ext x
  simp only [LinearMap.mem_ker, mulVecLin_apply, ‚Üê mulVec_mulVec]
  constructor
  ¬∑ intro h
    replace h := congr_arg (dotProduct x) h
    rwa [dotProduct_mulVec, dotProduct_zero, vecMul_transpose, dotProduct_self_eq_zero] at h
  ¬∑ intro h
    rw [h, mulVec_zero]
#align matrix.ker_mul_vec_lin_transpose_mul_self Matrix.ker_mulVecLin_transpose_mul_self

theorem rank_transpose_mul_self (A : Matrix m n R) : (A·µÄ ‚¨ù A).rank = A.rank := by
  dsimp only [rank]
  refine' add_left_injective (finrank R <| LinearMap.ker A.mulVecLin) _
  dsimp only
  trans finrank R { x // x ‚àà LinearMap.range (mulVecLin (A·µÄ ‚¨ù A)) } +
    finrank R { x // x ‚àà LinearMap.ker (mulVecLin (A·µÄ ‚¨ù A)) }
  ¬∑ rw [ker_mulVecLin_transpose_mul_self]
  ¬∑ simp only [LinearMap.finrank_range_add_finrank_ker]
#align matrix.rank_transpose_mul_self Matrix.rank_transpose_mul_self

/-- TODO: prove this in greater generality. -/
@[simp]
theorem rank_transpose (A : Matrix m n R) : A·µÄ.rank = A.rank :=
  le_antisymm ((rank_transpose_mul_self _).symm.trans_le <| rank_mul_le_left _ _)
    ((rank_transpose_mul_self _).symm.trans_le <| rank_mul_le_left _ _)
#align matrix.rank_transpose Matrix.rank_transpose

@[simp]
theorem rank_self_mul_transpose (A : Matrix m n R) : (A ‚¨ù A·µÄ).rank = A.rank := by
  simpa only [rank_transpose, transpose_transpose] using rank_transpose_mul_self A·µÄ
#align matrix.rank_self_mul_transpose Matrix.rank_self_mul_transpose

end LinearOrderedField

/-- The rank of a matrix is the rank of the space spanned by its rows.

TODO: prove this in a generality that works for `‚ÑÇ` too, not just `‚Ñö` and `‚Ñù`. -/
theorem rank_eq_finrank_span_row [LinearOrderedField R] [Finite m] (A : Matrix m n R) :
    A.rank = finrank R (Submodule.span R (Set.range A)) := by
  cases nonempty_fintype m
  rw [‚Üê rank_transpose, rank_eq_finrank_span_cols, transpose_transpose]
#align matrix.rank_eq_finrank_span_row Matrix.rank_eq_finrank_span_row

end Matrix
