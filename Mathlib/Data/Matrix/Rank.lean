/-
Copyright (c) 2021 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Eric Wieser
-/
import Mathlib.LinearAlgebra.FreeModule.Finite.Rank
import Mathlib.LinearAlgebra.Matrix.ToLin
import Mathlib.LinearAlgebra.FiniteDimensional
import Mathlib.LinearAlgebra.Matrix.DotProduct
import Mathlib.LinearAlgebra.Determinant
import Mathlib.LinearAlgebra.Matrix.Diagonal
import Mathlib.Data.Complex.Module

#align_import data.matrix.rank from "leanprover-community/mathlib"@"17219820a8aa8abe85adf5dfde19af1dd1bd8ae7"

/-!
# Rank of matrices

The rank of a matrix `A` is defined to be the rank of range of the linear map corresponding to `A`.
This definition does not depend on the choice of basis, see `Matrix.rank_eq_finrank_range_toLin`.

## Main declarations

* `Matrix.rank`: the rank of a matrix

## TODO

* Do a better job of generalizing over `â„š`, `â„`, and `â„‚` in `Matrix.rank_transpose` and
  `Matrix.rank_conjTranspose`. See
  [this Zulip thread](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/row.20rank.20equals.20column.20rank/near/350462992).

-/


open Matrix

namespace Matrix

open FiniteDimensional

variable {l m n o R : Type*} [m_fin : Fintype m] [Fintype n] [Fintype o]

section CommRing

variable [CommRing R]

/-- The rank of a matrix is the rank of its image. -/
noncomputable def rank (A : Matrix m n R) : â„• :=
  finrank R <| LinearMap.range A.mulVecLin
#align matrix.rank Matrix.rank

@[simp]
theorem rank_one [StrongRankCondition R] [DecidableEq n] :
    rank (1 : Matrix n n R) = Fintype.card n := by
  rw [rank, mulVecLin_one, LinearMap.range_id, finrank_top, finrank_pi]
  -- ğŸ‰ no goals
#align matrix.rank_one Matrix.rank_one

@[simp]
theorem rank_zero [Nontrivial R] : rank (0 : Matrix m n R) = 0 := by
  rw [rank, mulVecLin_zero, LinearMap.range_zero, finrank_bot]
  -- ğŸ‰ no goals
#align matrix.rank_zero Matrix.rank_zero

theorem rank_le_card_width [StrongRankCondition R] (A : Matrix m n R) :
    A.rank â‰¤ Fintype.card n := by
  haveI : Module.Finite R (n â†’ R) := Module.Finite.pi
  -- âŠ¢ rank A â‰¤ Fintype.card n
  haveI : Module.Free R (n â†’ R) := Module.Free.pi _ _
  -- âŠ¢ rank A â‰¤ Fintype.card n
  exact A.mulVecLin.finrank_range_le.trans_eq (finrank_pi _)
  -- ğŸ‰ no goals
#align matrix.rank_le_card_width Matrix.rank_le_card_width

theorem rank_le_width [StrongRankCondition R] {m n : â„•} (A : Matrix (Fin m) (Fin n) R) :
    A.rank â‰¤ n :=
  A.rank_le_card_width.trans <| (Fintype.card_fin n).le
#align matrix.rank_le_width Matrix.rank_le_width

theorem rank_mul_le_left [StrongRankCondition R] (A : Matrix m n R) (B : Matrix n o R) :
    (A * B).rank â‰¤ A.rank := by
  rw [rank, rank, mulVecLin_mul]
  -- âŠ¢ finrank R { x // x âˆˆ LinearMap.range (LinearMap.comp (mulVecLin A) (mulVecLi â€¦
  exact Cardinal.toNat_le_of_le_of_lt_aleph0 (rank_lt_aleph0 _ _) (LinearMap.rank_comp_le_left _ _)
  -- ğŸ‰ no goals
#align matrix.rank_mul_le_left Matrix.rank_mul_le_left

theorem rank_mul_le_right [StrongRankCondition R] (A : Matrix l m R) (B : Matrix m n R) :
    (A * B).rank â‰¤ B.rank := by
  rw [rank, rank, mulVecLin_mul]
  -- âŠ¢ finrank R { x // x âˆˆ LinearMap.range (LinearMap.comp (mulVecLin A) (mulVecLi â€¦
  exact
    finrank_le_finrank_of_rank_le_rank (LinearMap.lift_rank_comp_le_right _ _) (rank_lt_aleph0 _ _)
#align matrix.rank_mul_le_right Matrix.rank_mul_le_right

theorem rank_mul_le [StrongRankCondition R] (A : Matrix m n R) (B : Matrix n o R) :
    (A * B).rank â‰¤ min A.rank B.rank :=
  le_min (rank_mul_le_left _ _) (rank_mul_le_right _ _)
#align matrix.rank_mul_le Matrix.rank_mul_le

theorem rank_unit [StrongRankCondition R] [DecidableEq n] (A : (Matrix n n R)Ë£) :
    (A : Matrix n n R).rank = Fintype.card n := by
  refine' le_antisymm (rank_le_card_width A) _
  -- âŠ¢ Fintype.card n â‰¤ rank â†‘A
  have := rank_mul_le_left (A : Matrix n n R) (â†‘Aâ»Â¹ : Matrix n n R)
  -- âŠ¢ Fintype.card n â‰¤ rank â†‘A
  rwa [â† Units.val_mul, mul_inv_self, Units.val_one, rank_one] at this
  -- ğŸ‰ no goals
#align matrix.rank_unit Matrix.rank_unit

theorem rank_of_isUnit [StrongRankCondition R] [DecidableEq n] (A : Matrix n n R) (h : IsUnit A) :
    A.rank = Fintype.card n := by
  obtain âŸ¨A, rflâŸ© := h
  -- âŠ¢ rank â†‘A = Fintype.card n
  exact rank_unit A
  -- ğŸ‰ no goals
#align matrix.rank_of_is_unit Matrix.rank_of_isUnit

/-- Right multiplying by an invertible matrix does not change the rank -/
lemma rank_mul_eq_left_of_isUnit_det [DecidableEq n]
    (A : Matrix n n R) (B : Matrix m n R) (hA : IsUnit A.det) :
    (B * A).rank = B.rank := by
  suffices : Function.Surjective A.mulVecLin
  -- âŠ¢ rank (B * A) = rank B
  Â· rw [rank, mulVecLin_mul, LinearMap.range_comp_of_range_eq_top _
    (LinearMap.range_eq_top.mpr this), â† rank]
  intro v
  -- âŠ¢ âˆƒ a, â†‘(mulVecLin A) a = v
  exact âŸ¨(Aâ»Â¹).mulVecLin v, by simp [mul_nonsing_inv _ hA]âŸ©
  -- ğŸ‰ no goals

/-- Left multiplying by an invertible matrix does not change the rank -/
lemma rank_mul_eq_right_of_isUnit_det [DecidableEq m]
    (A : Matrix m m R) (B : Matrix m n R) (hA : IsUnit A.det) :
    (A * B).rank = B.rank := by
  let b : Basis m R (m â†’ R) := Pi.basisFun R m
  -- âŠ¢ rank (A * B) = rank B
  replace hA : IsUnit (LinearMap.toMatrix b b A.mulVecLin).det := by
    convert hA; rw [â† LinearEquiv.eq_symm_apply]; rfl
  have hAB : mulVecLin (A * B) = (LinearEquiv.ofIsUnitDet hA).comp (mulVecLin B) := by ext; simp
  -- âŠ¢ rank (A * B) = rank B
  rw [rank, rank, hAB, LinearMap.range_comp, LinearEquiv.finrank_map_eq]
  -- ğŸ‰ no goals

/-- Taking a subset of the rows and permuting the columns reduces the rank. -/
theorem rank_submatrix_le [StrongRankCondition R] [Fintype m] (f : n â†’ m) (e : n â‰ƒ m)
    (A : Matrix m m R) : rank (A.submatrix f e) â‰¤ rank A := by
  rw [rank, rank, mulVecLin_submatrix, LinearMap.range_comp, LinearMap.range_comp,
    show LinearMap.funLeft R R e.symm = LinearEquiv.funCongrLeft R R e.symm from rfl,
    LinearEquiv.range, Submodule.map_top]
  exact Submodule.finrank_map_le _ _
  -- ğŸ‰ no goals
#align matrix.rank_submatrix_le Matrix.rank_submatrix_le

theorem rank_reindex [Fintype m] (eâ‚ eâ‚‚ : m â‰ƒ n) (A : Matrix m m R) :
    rank (reindex eâ‚ eâ‚‚ A) = rank A := by
  rw [rank, rank, mulVecLin_reindex, LinearMap.range_comp, LinearMap.range_comp,
    LinearEquiv.range, Submodule.map_top, LinearEquiv.finrank_map_eq]
#align matrix.rank_reindex Matrix.rank_reindex

@[simp]
theorem rank_submatrix [Fintype m] (A : Matrix m m R) (eâ‚ eâ‚‚ : n â‰ƒ m) :
    rank (A.submatrix eâ‚ eâ‚‚) = rank A := by
  simpa only [reindex_apply] using rank_reindex eâ‚.symm eâ‚‚.symm A
  -- ğŸ‰ no goals
#align matrix.rank_submatrix Matrix.rank_submatrix

theorem rank_eq_finrank_range_toLin [DecidableEq n] {Mâ‚ Mâ‚‚ : Type*} [AddCommGroup Mâ‚]
    [AddCommGroup Mâ‚‚] [Module R Mâ‚] [Module R Mâ‚‚] (A : Matrix m n R) (vâ‚ : Basis m R Mâ‚)
    (vâ‚‚ : Basis n R Mâ‚‚) : A.rank = finrank R (LinearMap.range (toLin vâ‚‚ vâ‚ A)) := by
  let eâ‚ := (Pi.basisFun R m).equiv vâ‚ (Equiv.refl _)
  -- âŠ¢ rank A = finrank R { x // x âˆˆ LinearMap.range (â†‘(toLin vâ‚‚ vâ‚) A) }
  let eâ‚‚ := (Pi.basisFun R n).equiv vâ‚‚ (Equiv.refl _)
  -- âŠ¢ rank A = finrank R { x // x âˆˆ LinearMap.range (â†‘(toLin vâ‚‚ vâ‚) A) }
  have range_eâ‚‚ : LinearMap.range eâ‚‚ = âŠ¤ := by
    rw [LinearMap.range_eq_top]
    exact eâ‚‚.surjective
  refine' LinearEquiv.finrank_eq (eâ‚.ofSubmodules _ _ _)
  -- âŠ¢ Submodule.map (â†‘eâ‚) (LinearMap.range (mulVecLin A)) = LinearMap.range (â†‘(toL â€¦
  rw [â† LinearMap.range_comp, â† LinearMap.range_comp_of_range_eq_top (toLin vâ‚‚ vâ‚ A) range_eâ‚‚]
  -- âŠ¢ LinearMap.range (LinearMap.comp (â†‘eâ‚) (mulVecLin A)) = LinearMap.range (Line â€¦
  congr 1
  -- âŠ¢ LinearMap.comp (â†‘eâ‚) (mulVecLin A) = LinearMap.comp (â†‘(toLin vâ‚‚ vâ‚) A) â†‘eâ‚‚
  apply LinearMap.pi_ext'
  -- âŠ¢ âˆ€ (i : n), LinearMap.comp (LinearMap.comp (â†‘eâ‚) (mulVecLin A)) (LinearMap.si â€¦
  rintro i
  -- âŠ¢ LinearMap.comp (LinearMap.comp (â†‘eâ‚) (mulVecLin A)) (LinearMap.single i) = L â€¦
  apply LinearMap.ext_ring
  -- âŠ¢ â†‘(LinearMap.comp (LinearMap.comp (â†‘eâ‚) (mulVecLin A)) (LinearMap.single i))  â€¦
  have auxâ‚ := toLin_self (Pi.basisFun R n) (Pi.basisFun R m) A i
  -- âŠ¢ â†‘(LinearMap.comp (LinearMap.comp (â†‘eâ‚) (mulVecLin A)) (LinearMap.single i))  â€¦
  have auxâ‚‚ := Basis.equiv_apply (Pi.basisFun R n) i vâ‚‚
  -- âŠ¢ â†‘(LinearMap.comp (LinearMap.comp (â†‘eâ‚) (mulVecLin A)) (LinearMap.single i))  â€¦
  rw [toLin_eq_toLin', toLin'_apply'] at auxâ‚
  -- âŠ¢ â†‘(LinearMap.comp (LinearMap.comp (â†‘eâ‚) (mulVecLin A)) (LinearMap.single i))  â€¦
  rw [Pi.basisFun_apply, LinearMap.coe_stdBasis] at auxâ‚ auxâ‚‚
  -- âŠ¢ â†‘(LinearMap.comp (LinearMap.comp (â†‘eâ‚) (mulVecLin A)) (LinearMap.single i))  â€¦
  simp only [LinearMap.comp_apply, LinearEquiv.coe_coe, Equiv.refl_apply, auxâ‚, auxâ‚‚,
    LinearMap.coe_single, toLin_self, LinearEquiv.map_sum, LinearEquiv.map_smul, Basis.equiv_apply]
#align matrix.rank_eq_finrank_range_to_lin Matrix.rank_eq_finrank_range_toLin

theorem rank_le_card_height [StrongRankCondition R] (A : Matrix m n R) :
    A.rank â‰¤ Fintype.card m := by
  haveI : Module.Finite R (m â†’ R) := Module.Finite.pi
  -- âŠ¢ rank A â‰¤ Fintype.card m
  haveI : Module.Free R (m â†’ R) := Module.Free.pi _ _
  -- âŠ¢ rank A â‰¤ Fintype.card m
  exact (Submodule.finrank_le _).trans (finrank_pi R).le
  -- ğŸ‰ no goals
#align matrix.rank_le_card_height Matrix.rank_le_card_height

theorem rank_le_height [StrongRankCondition R] {m n : â„•} (A : Matrix (Fin m) (Fin n) R) :
    A.rank â‰¤ m :=
  A.rank_le_card_height.trans <| (Fintype.card_fin m).le
#align matrix.rank_le_height Matrix.rank_le_height

/-- The rank of a matrix is the rank of the space spanned by its columns. -/
theorem rank_eq_finrank_span_cols (A : Matrix m n R) :
    A.rank = finrank R (Submodule.span R (Set.range Aáµ€)) := by rw [rank, Matrix.range_mulVecLin]
                                                               -- ğŸ‰ no goals
#align matrix.rank_eq_finrank_span_cols Matrix.rank_eq_finrank_span_cols

end CommRing

section Field

variable [Field R]

/-- The rank of a diagnonal matrix is the count of non-zero elements on its main diagonal -/
theorem rank_diagonal [DecidableEq m] [DecidableEq R] (w : m â†’ R) :
    (diagonal w).rank = Fintype.card {i // (w i) â‰  0} := by
  rw [Matrix.rank, â† Matrix.toLin'_apply', FiniteDimensional.finrank, â† LinearMap.rank,
    LinearMap.rank_diagonal, Cardinal.toNat_cast]

end Field

/-! ### Lemmas about transpose and conjugate transpose

This section contains lemmas about the rank of `Matrix.transpose` and `Matrix.conjTranspose`.

Unfortunately the proofs are essentially duplicated between the two; `â„š` is a linearly-ordered ring
but can't be a star-ordered ring, while `â„‚` is star-ordered (with `open ComplexOrder`) but
not linearly ordered. For now we don't prove the transpose case for `â„‚`.

TODO: the lemmas `Matrix.rank_transpose` and `Matrix.rank_conjTranspose` current follow a short
proof that is a simple consequence of `Matrix.rank_transpose_mul_self` and
`Matrix.rank_conjTranspose_mul_self`. This proof pulls in unnecessary assumptions on `R`, and should
be replaced with a proof that uses Gaussian reduction or argues via linear combinations.
-/

section StarOrderedField

variable [Fintype m] [Field R] [PartialOrder R] [StarOrderedRing R]

theorem ker_mulVecLin_conjTranspose_mul_self (A : Matrix m n R) :
    LinearMap.ker (Aá´´ * A).mulVecLin = LinearMap.ker (mulVecLin A) := by
  ext x
  -- âŠ¢ x âˆˆ LinearMap.ker (mulVecLin (Aá´´ * A)) â†” x âˆˆ LinearMap.ker (mulVecLin A)
  simp only [LinearMap.mem_ker, mulVecLin_apply, conjTranspose_mul_self_mulVec_eq_zero]
  -- ğŸ‰ no goals
#align matrix.ker_mul_vec_lin_conj_transpose_mul_self Matrix.ker_mulVecLin_conjTranspose_mul_self

theorem rank_conjTranspose_mul_self (A : Matrix m n R) : (Aá´´ * A).rank = A.rank := by
  dsimp only [rank]
  -- âŠ¢ finrank R { x // x âˆˆ LinearMap.range (mulVecLin (Aá´´ * A)) } = finrank R { x  â€¦
  refine' add_left_injective (finrank R (LinearMap.ker (mulVecLin A))) _
  -- âŠ¢ (fun x => x + finrank R { x // x âˆˆ LinearMap.ker (mulVecLin A) }) (finrank R â€¦
  dsimp only
  -- âŠ¢ finrank R { x // x âˆˆ LinearMap.range (mulVecLin (Aá´´ * A)) } + finrank R { x  â€¦
  trans finrank R { x // x âˆˆ LinearMap.range (mulVecLin (Aá´´ * A)) } +
    finrank R { x // x âˆˆ LinearMap.ker (mulVecLin (Aá´´ * A)) }
  Â· rw [ker_mulVecLin_conjTranspose_mul_self]
    -- ğŸ‰ no goals
  Â· simp only [LinearMap.finrank_range_add_finrank_ker]
    -- ğŸ‰ no goals
#align matrix.rank_conj_transpose_mul_self Matrix.rank_conjTranspose_mul_self

-- this follows the proof here https://math.stackexchange.com/a/81903/1896
/-- TODO: prove this in greater generality. -/
@[simp]
theorem rank_conjTranspose (A : Matrix m n R) : Aá´´.rank = A.rank :=
  le_antisymm
    (((rank_conjTranspose_mul_self _).symm.trans_le <| rank_mul_le_left _ _).trans_eq <|
      congr_arg _ <| conjTranspose_conjTranspose _)
    ((rank_conjTranspose_mul_self _).symm.trans_le <| rank_mul_le_left _ _)
#align matrix.rank_conj_transpose Matrix.rank_conjTranspose

@[simp]
theorem rank_self_mul_conjTranspose (A : Matrix m n R) : (A * Aá´´).rank = A.rank := by
  simpa only [rank_conjTranspose, conjTranspose_conjTranspose] using
    rank_conjTranspose_mul_self Aá´´
#align matrix.rank_self_mul_conj_transpose Matrix.rank_self_mul_conjTranspose

end StarOrderedField

section LinearOrderedField

variable [Fintype m] [LinearOrderedField R]

theorem ker_mulVecLin_transpose_mul_self (A : Matrix m n R) :
    LinearMap.ker (Aáµ€ * A).mulVecLin = LinearMap.ker (mulVecLin A) := by
  ext x
  -- âŠ¢ x âˆˆ LinearMap.ker (mulVecLin (Aáµ€ * A)) â†” x âˆˆ LinearMap.ker (mulVecLin A)
  simp only [LinearMap.mem_ker, mulVecLin_apply, â† mulVec_mulVec]
  -- âŠ¢ mulVec Aáµ€ (mulVec A x) = 0 â†” mulVec A x = 0
  constructor
  -- âŠ¢ mulVec Aáµ€ (mulVec A x) = 0 â†’ mulVec A x = 0
  Â· intro h
    -- âŠ¢ mulVec A x = 0
    replace h := congr_arg (dotProduct x) h
    -- âŠ¢ mulVec A x = 0
    rwa [dotProduct_mulVec, dotProduct_zero, vecMul_transpose, dotProduct_self_eq_zero] at h
    -- ğŸ‰ no goals
  Â· intro h
    -- âŠ¢ mulVec Aáµ€ (mulVec A x) = 0
    rw [h, mulVec_zero]
    -- ğŸ‰ no goals
#align matrix.ker_mul_vec_lin_transpose_mul_self Matrix.ker_mulVecLin_transpose_mul_self

theorem rank_transpose_mul_self (A : Matrix m n R) : (Aáµ€ * A).rank = A.rank := by
  dsimp only [rank]
  -- âŠ¢ finrank R { x // x âˆˆ LinearMap.range (mulVecLin (Aáµ€ * A)) } = finrank R { x  â€¦
  refine' add_left_injective (finrank R <| LinearMap.ker A.mulVecLin) _
  -- âŠ¢ (fun x => x + finrank R { x // x âˆˆ LinearMap.ker (mulVecLin A) }) (finrank R â€¦
  dsimp only
  -- âŠ¢ finrank R { x // x âˆˆ LinearMap.range (mulVecLin (Aáµ€ * A)) } + finrank R { x  â€¦
  trans finrank R { x // x âˆˆ LinearMap.range (mulVecLin (Aáµ€ * A)) } +
    finrank R { x // x âˆˆ LinearMap.ker (mulVecLin (Aáµ€ * A)) }
  Â· rw [ker_mulVecLin_transpose_mul_self]
    -- ğŸ‰ no goals
  Â· simp only [LinearMap.finrank_range_add_finrank_ker]
    -- ğŸ‰ no goals
#align matrix.rank_transpose_mul_self Matrix.rank_transpose_mul_self

/-- TODO: prove this in greater generality. -/
@[simp]
theorem rank_transpose (A : Matrix m n R) : Aáµ€.rank = A.rank :=
  le_antisymm ((rank_transpose_mul_self _).symm.trans_le <| rank_mul_le_left _ _)
    ((rank_transpose_mul_self _).symm.trans_le <| rank_mul_le_left _ _)
#align matrix.rank_transpose Matrix.rank_transpose

@[simp]
theorem rank_self_mul_transpose (A : Matrix m n R) : (A * Aáµ€).rank = A.rank := by
  simpa only [rank_transpose, transpose_transpose] using rank_transpose_mul_self Aáµ€
  -- ğŸ‰ no goals
#align matrix.rank_self_mul_transpose Matrix.rank_self_mul_transpose

end LinearOrderedField

/-- The rank of a matrix is the rank of the space spanned by its rows.

TODO: prove this in a generality that works for `â„‚` too, not just `â„š` and `â„`. -/
theorem rank_eq_finrank_span_row [LinearOrderedField R] [Finite m] (A : Matrix m n R) :
    A.rank = finrank R (Submodule.span R (Set.range A)) := by
  cases nonempty_fintype m
  -- âŠ¢ rank A = finrank R { x // x âˆˆ Submodule.span R (Set.range A) }
  rw [â† rank_transpose, rank_eq_finrank_span_cols, transpose_transpose]
  -- ğŸ‰ no goals
#align matrix.rank_eq_finrank_span_row Matrix.rank_eq_finrank_span_row

end Matrix
