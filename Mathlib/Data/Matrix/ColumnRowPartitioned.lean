/-
Copyright (c) 2023 Mohanad ahmed. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mohanad Ahmed
-/

import Mathlib.Data.Matrix.Basic
import Mathlib.Data.Matrix.Block
import Mathlib.LinearAlgebra.Matrix.NonsingularInverse

/-! # Block Matrices from Rows and Columns

This file provides the basic definitions of matrices composed from columns and rows.
The concatenation of two matrices with the same row indices can be expressed as
`A = fromColumns A‚ÇÅ A‚ÇÇ` the concatenation of two matrices with the same column indices
can be expressed as `B = fromRows B‚ÇÅ B‚ÇÇ`.

We then provide a few lemmas that deal with the products of these with each other and
with block matrices

## Tags
column matrices, row matrices, column row block matrices
-/

namespace Matrix

variable {R : Type*}
variable {m m‚ÇÅ m‚ÇÇ n n‚ÇÅ n‚ÇÇ : Type*}
variable [Fintype m] [Fintype m‚ÇÅ] [Fintype m‚ÇÇ]
variable [Fintype n] [Fintype n‚ÇÅ] [Fintype n‚ÇÇ]
variable [DecidableEq m] [DecidableEq m‚ÇÅ] [DecidableEq m‚ÇÇ]
variable [DecidableEq n] [DecidableEq n‚ÇÅ] [DecidableEq n‚ÇÇ]

/-- Concatenate together two matrices A‚ÇÅ[m‚ÇÅ √ó N] and A‚ÇÇ[m‚ÇÇ √ó N] with the same columns (N) to get a
bigger matrix indexed by [(m‚ÇÅ ‚äï m‚ÇÇ) √ó N] -/
def fromRows (A‚ÇÅ : Matrix m‚ÇÅ n R) (A‚ÇÇ : Matrix m‚ÇÇ n R) : Matrix (m‚ÇÅ ‚äï m‚ÇÇ) n R :=
  of (Sum.elim A‚ÇÅ A‚ÇÇ)

/-- Concatenate together two matrices B‚ÇÅ[m √ó n‚ÇÅ] and B‚ÇÇ[m √ó n‚ÇÇ] with the same rows (M) to get a
bigger matrix indexed by [m √ó (n‚ÇÅ ‚äï n‚ÇÇ)] -/
def fromColumns (B‚ÇÅ : Matrix m n‚ÇÅ R) (B‚ÇÇ : Matrix m n‚ÇÇ R) : Matrix m (n‚ÇÅ ‚äï n‚ÇÇ) R :=
  of fun i => Sum.elim (B‚ÇÅ i) (B‚ÇÇ i)

/-- Given a column partitioned matrix extract the first column -/
def toColumns‚ÇÅ (A : Matrix m (n‚ÇÅ ‚äï n‚ÇÇ) R) : Matrix m n‚ÇÅ R := of fun i j => (A i (Sum.inl j))

/-- Given a column partitioned matrix extract the second column -/
def toColumns‚ÇÇ (A : Matrix m (n‚ÇÅ ‚äï n‚ÇÇ) R) : Matrix m n‚ÇÇ R := of fun i j => (A i (Sum.inr j))

/-- Given a row partitioned matrix extract the first row -/
def toRows‚ÇÅ (A : Matrix (m‚ÇÅ ‚äï m‚ÇÇ) n R) : Matrix m‚ÇÅ n R := of fun i j => (A (Sum.inl i) j)

/-- Given a row partitioned matrix extract the second row -/
def toRows‚ÇÇ (A : Matrix (m‚ÇÅ ‚äï m‚ÇÇ) n R) : Matrix m‚ÇÇ n R := of fun i j => (A (Sum.inr i) j)

@[simp]
lemma fromRows_apply_inl (A‚ÇÅ : Matrix m‚ÇÅ n R) (A‚ÇÇ : Matrix m‚ÇÇ n R) (i : m‚ÇÅ) (j : n) :
    (fromRows A‚ÇÅ A‚ÇÇ) (Sum.inl i) j = A‚ÇÅ i j := rfl

@[simp]
lemma fromRows_apply_inr (A‚ÇÅ : Matrix m‚ÇÅ n R) (A‚ÇÇ : Matrix m‚ÇÇ n R) (i : m‚ÇÇ) (j : n) :
    (fromRows A‚ÇÅ A‚ÇÇ) (Sum.inr i) j = A‚ÇÇ i j := rfl

@[simp]
lemma fromColumns_apply_inl (A‚ÇÅ : Matrix m n‚ÇÅ R) (A‚ÇÇ : Matrix m n‚ÇÇ R) (i : m) (j : n‚ÇÅ) :
    (fromColumns A‚ÇÅ A‚ÇÇ) i (Sum.inl j) = A‚ÇÅ i j := rfl

@[simp]
lemma fromColumns_apply_inr (A‚ÇÅ : Matrix m n‚ÇÅ R) (A‚ÇÇ : Matrix m n‚ÇÇ R) (i : m) (j : n‚ÇÇ) :
    (fromColumns A‚ÇÅ A‚ÇÇ) i (Sum.inr j) = A‚ÇÇ i j := rfl

@[simp]
lemma toRows‚ÇÅ_apply (A : Matrix (m‚ÇÅ ‚äï m‚ÇÇ) n R) (i : m‚ÇÅ) (j : n) :
    (toRows‚ÇÅ A) i j = A (Sum.inl i) j := rfl

@[simp]
lemma toRows‚ÇÇ_apply (A : Matrix (m‚ÇÅ ‚äï m‚ÇÇ) n R) (i : m‚ÇÇ) (j : n) :
    (toRows‚ÇÇ A) i j = A (Sum.inr i) j := rfl

@[simp]
lemma toRows‚ÇÅ_fromRows  (A‚ÇÅ : Matrix m‚ÇÅ n R) (A‚ÇÇ : Matrix m‚ÇÇ n R) :
    toRows‚ÇÅ (fromRows A‚ÇÅ A‚ÇÇ) = A‚ÇÅ := rfl

@[simp]
lemma toRows‚ÇÇ_fromRows  (A‚ÇÅ : Matrix m‚ÇÅ n R) (A‚ÇÇ : Matrix m‚ÇÇ n R) :
    toRows‚ÇÇ (fromRows A‚ÇÅ A‚ÇÇ) = A‚ÇÇ := rfl

@[simp]
lemma toColumns‚ÇÅ_apply (A : Matrix m (n‚ÇÅ ‚äï n‚ÇÇ) R) (i : m) (j : n‚ÇÅ) :
    (toColumns‚ÇÅ A) i j = A i (Sum.inl j) := rfl

@[simp]
lemma toColumns‚ÇÇ_apply (A : Matrix m (n‚ÇÅ ‚äï n‚ÇÇ) R) (i : m) (j : n‚ÇÇ) :
    (toColumns‚ÇÇ A) i j = A i (Sum.inr j) := rfl

@[simp]
lemma toColumns‚ÇÅ_fromColumns  (A‚ÇÅ : Matrix m n‚ÇÅ R) (A‚ÇÇ : Matrix m n‚ÇÇ R) :
    toColumns‚ÇÅ (fromColumns A‚ÇÅ A‚ÇÇ) = A‚ÇÅ := rfl

@[simp]
lemma toColumns‚ÇÇ_fromColumns  (A‚ÇÅ : Matrix m n‚ÇÅ R) (A‚ÇÇ : Matrix m n‚ÇÇ R) :
    toColumns‚ÇÇ (fromColumns A‚ÇÅ A‚ÇÇ) = A‚ÇÇ := rfl

@[simp]
lemma fromColumns_toColumns (A : Matrix m (n‚ÇÅ ‚äï n‚ÇÇ) R) :
    fromColumns A.toColumns‚ÇÅ A.toColumns‚ÇÇ = A := by
  ext i (j | j) <;> simp
  -- ‚ä¢ fromColumns (toColumns‚ÇÅ A) (toColumns‚ÇÇ A) i (Sum.inl j) = A i (Sum.inl j)
                    -- üéâ no goals
                    -- üéâ no goals

@[simp]
lemma fromRows_toRows (A : Matrix (m‚ÇÅ ‚äï m‚ÇÇ) n R) : fromRows A.toRows‚ÇÅ A.toRows‚ÇÇ = A := by
  ext (i | i) j <;> simp
  -- ‚ä¢ fromRows (toRows‚ÇÅ A) (toRows‚ÇÇ A) (Sum.inl i) j = A (Sum.inl i) j
                    -- üéâ no goals
                    -- üéâ no goals

lemma fromRows_inj : Function.Injective2 (@fromRows R m‚ÇÅ m‚ÇÇ n) := by
  intros x1 x2 y1 y2
  -- ‚ä¢ fromRows x1 y1 = fromRows x2 y2 ‚Üí x1 = x2 ‚àß y1 = y2
  simp only [Function.funext_iff, ‚Üê Matrix.ext_iff]
  -- ‚ä¢ (‚àÄ (i : m‚ÇÅ ‚äï m‚ÇÇ) (j : n), fromRows x1 y1 i j = fromRows x2 y2 i j) ‚Üí (‚àÄ (i : ‚Ä¶
  aesop
  -- üéâ no goals

lemma fromColumns_inj : Function.Injective2 (@fromColumns R m n‚ÇÅ n‚ÇÇ) := by
  intros x1 x2 y1 y2
  -- ‚ä¢ fromColumns x1 y1 = fromColumns x2 y2 ‚Üí x1 = x2 ‚àß y1 = y2
  simp only [Function.funext_iff, ‚Üê Matrix.ext_iff]
  -- ‚ä¢ (‚àÄ (i : m) (j : n‚ÇÅ ‚äï n‚ÇÇ), fromColumns x1 y1 i j = fromColumns x2 y2 i j) ‚Üí ( ‚Ä¶
  aesop
  -- üéâ no goals

lemma fromColumns_ext_iff (A‚ÇÅ : Matrix m n‚ÇÅ R) (A‚ÇÇ : Matrix m n‚ÇÇ R) (B‚ÇÅ : Matrix m n‚ÇÅ R)
    (B‚ÇÇ : Matrix m n‚ÇÇ R) :
    fromColumns A‚ÇÅ A‚ÇÇ = fromColumns B‚ÇÅ B‚ÇÇ ‚Üî A‚ÇÅ = B‚ÇÅ ‚àß A‚ÇÇ = B‚ÇÇ := fromColumns_inj.eq_iff

lemma fromRows_ext_iff (A‚ÇÅ : Matrix m‚ÇÅ n R) (A‚ÇÇ : Matrix m‚ÇÇ n R) (B‚ÇÅ : Matrix m‚ÇÅ n R)
    (B‚ÇÇ : Matrix m‚ÇÇ n R) :
    fromRows A‚ÇÅ A‚ÇÇ = fromRows B‚ÇÅ B‚ÇÇ ‚Üî A‚ÇÅ = B‚ÇÅ ‚àß A‚ÇÇ = B‚ÇÇ := fromRows_inj.eq_iff

/- A column partioned matrix when transposed gives a row partioned matrix with columns of the
initial matrix tranposed to become rows. -/
lemma transpose_fromColumns (A‚ÇÅ : Matrix m n‚ÇÅ R) (A‚ÇÇ : Matrix m n‚ÇÇ R) :
    transpose (fromColumns A‚ÇÅ A‚ÇÇ) = fromRows (transpose A‚ÇÅ) (transpose A‚ÇÇ) := by
  ext (i | i) j <;> simp
  -- ‚ä¢ (fromColumns A‚ÇÅ A‚ÇÇ)·µÄ (Sum.inl i) j = fromRows A‚ÇÅ·µÄ A‚ÇÇ·µÄ (Sum.inl i) j
                    -- üéâ no goals
                    -- üéâ no goals

/- A row partioned matrix when transposed gives a column partioned matrix with rows of the initial
matrix tranposed to become columns. -/
lemma transpose_fromRows (A‚ÇÅ : Matrix m‚ÇÅ n R) (A‚ÇÇ : Matrix m‚ÇÇ n R) :
    transpose (fromRows A‚ÇÅ A‚ÇÇ) = fromColumns (transpose A‚ÇÅ) (transpose A‚ÇÇ) := by
  ext i (j | j) <;> simp
  -- ‚ä¢ (fromRows A‚ÇÅ A‚ÇÇ)·µÄ i (Sum.inl j) = fromColumns A‚ÇÅ·µÄ A‚ÇÇ·µÄ i (Sum.inl j)
                    -- üéâ no goals
                    -- üéâ no goals

section Semiring

variable [Semiring R]

@[simp]
lemma fromRows_mul (A‚ÇÅ : Matrix m‚ÇÅ n R) (A‚ÇÇ : Matrix m‚ÇÇ n R) (B : Matrix n m R) :
    (fromRows A‚ÇÅ A‚ÇÇ) * B = fromRows (A‚ÇÅ * B) (A‚ÇÇ * B) := by
  ext (_ | _) _ <;> simp [mul_apply]
  -- ‚ä¢ (fromRows A‚ÇÅ A‚ÇÇ * B) (Sum.inl val‚úù) x‚úù = fromRows (A‚ÇÅ * B) (A‚ÇÇ * B) (Sum.inl ‚Ä¶
                    -- üéâ no goals
                    -- üéâ no goals

@[simp]
lemma mul_fromColumns (A : Matrix m n R) (B‚ÇÅ : Matrix n n‚ÇÅ R) (B‚ÇÇ : Matrix n n‚ÇÇ R) :
    A * (fromColumns B‚ÇÅ B‚ÇÇ) = fromColumns (A * B‚ÇÅ) (A * B‚ÇÇ) := by
  ext _ (_ | _) <;> simp [mul_apply]
  -- ‚ä¢ (A * fromColumns B‚ÇÅ B‚ÇÇ) i‚úù (Sum.inl val‚úù) = fromColumns (A * B‚ÇÅ) (A * B‚ÇÇ) i‚úù ‚Ä¶
                    -- üéâ no goals
                    -- üéâ no goals

@[simp]
lemma fromRows_zero : fromRows (0 : Matrix m‚ÇÅ n R) (0 : Matrix m‚ÇÇ n R) = 0 := by
  ext (_ | _) _ <;> simp
  -- ‚ä¢ fromRows 0 0 (Sum.inl val‚úù) x‚úù = OfNat.ofNat 0 (Sum.inl val‚úù) x‚úù
                    -- üéâ no goals
                    -- üéâ no goals

@[simp]
lemma fromColumns_zero : fromColumns (0 : Matrix m n‚ÇÅ R) (0 : Matrix m n‚ÇÇ R) = 0 := by
  ext _ (_ | _) <;> simp
  -- ‚ä¢ fromColumns 0 0 i‚úù (Sum.inl val‚úù) = OfNat.ofNat 0 i‚úù (Sum.inl val‚úù)
                    -- üéâ no goals
                    -- üéâ no goals

@[simp]
lemma fromColumns_fromRows_eq_fromBlocks (B‚ÇÅ‚ÇÅ : Matrix m‚ÇÅ n‚ÇÅ R) (B‚ÇÅ‚ÇÇ : Matrix m‚ÇÅ n‚ÇÇ R)
    (B‚ÇÇ‚ÇÅ : Matrix m‚ÇÇ n‚ÇÅ R) (B‚ÇÇ‚ÇÇ : Matrix m‚ÇÇ n‚ÇÇ R) :
    fromColumns (fromRows B‚ÇÅ‚ÇÅ B‚ÇÇ‚ÇÅ) (fromRows B‚ÇÅ‚ÇÇ B‚ÇÇ‚ÇÇ) = fromBlocks B‚ÇÅ‚ÇÅ B‚ÇÅ‚ÇÇ B‚ÇÇ‚ÇÅ B‚ÇÇ‚ÇÇ := by
  ext (_ | _) (_ | _) <;> simp
                          -- üéâ no goals
                          -- üéâ no goals
                          -- üéâ no goals
                          -- üéâ no goals

@[simp]
lemma fromRows_fromColumn_eq_fromBlocks (B‚ÇÅ‚ÇÅ : Matrix m‚ÇÅ n‚ÇÅ R) (B‚ÇÅ‚ÇÇ : Matrix m‚ÇÅ n‚ÇÇ R)
    (B‚ÇÇ‚ÇÅ : Matrix m‚ÇÇ n‚ÇÅ R) (B‚ÇÇ‚ÇÇ : Matrix m‚ÇÇ n‚ÇÇ R) :
    fromRows (fromColumns B‚ÇÅ‚ÇÅ B‚ÇÅ‚ÇÇ) (fromColumns B‚ÇÇ‚ÇÅ B‚ÇÇ‚ÇÇ) = fromBlocks B‚ÇÅ‚ÇÅ B‚ÇÅ‚ÇÇ B‚ÇÇ‚ÇÅ B‚ÇÇ‚ÇÇ := by
  ext (_ | _) (_ | _) <;> simp
                          -- üéâ no goals
                          -- üéâ no goals
                          -- üéâ no goals
                          -- üéâ no goals

/-- A row partitioned matrix multiplied by a column partioned matrix gives a 2 by 2 block matrix -/
lemma fromRows_mul_fromColumns (A‚ÇÅ : Matrix m‚ÇÅ n R) (A‚ÇÇ : Matrix m‚ÇÇ n R)
    (B‚ÇÅ : Matrix n n‚ÇÅ R) (B‚ÇÇ : Matrix n n‚ÇÇ R) :
    (fromRows A‚ÇÅ A‚ÇÇ) * (fromColumns B‚ÇÅ B‚ÇÇ) =
      fromBlocks (A‚ÇÅ * B‚ÇÅ) (A‚ÇÅ * B‚ÇÇ) (A‚ÇÇ * B‚ÇÅ) (A‚ÇÇ * B‚ÇÇ) := by
  ext (_ | _) (_ | _) <;> simp
                          -- üéâ no goals
                          -- üéâ no goals
                          -- üéâ no goals
                          -- üéâ no goals

/-- A column partitioned matrix mulitplied by a row partitioned matrix gives the sum of the "outer"
products of the block matrices -/
lemma fromColumns_mul_fromRows (A‚ÇÅ : Matrix m n‚ÇÅ R) (A‚ÇÇ : Matrix m n‚ÇÇ R)
    (B‚ÇÅ : Matrix n‚ÇÅ n R) (B‚ÇÇ : Matrix n‚ÇÇ n R) :
    fromColumns A‚ÇÅ A‚ÇÇ * fromRows B‚ÇÅ B‚ÇÇ = (A‚ÇÅ * B‚ÇÅ + A‚ÇÇ * B‚ÇÇ) := by
  ext
  -- ‚ä¢ (fromColumns A‚ÇÅ A‚ÇÇ * fromRows B‚ÇÅ B‚ÇÇ) i‚úù x‚úù = (A‚ÇÅ * B‚ÇÅ + A‚ÇÇ * B‚ÇÇ) i‚úù x‚úù
  simp [mul_apply]
  -- üéâ no goals

/-- A column partitioned matrix multipiled by a block matrix results in a column partioned matrix -/
lemma fromColumns_mul_fromBlocks (A‚ÇÅ : Matrix m m‚ÇÅ R) (A‚ÇÇ : Matrix m m‚ÇÇ R)
    (B‚ÇÅ‚ÇÅ : Matrix m‚ÇÅ n‚ÇÅ R) (B‚ÇÅ‚ÇÇ : Matrix m‚ÇÅ n‚ÇÇ R) (B‚ÇÇ‚ÇÅ : Matrix m‚ÇÇ n‚ÇÅ R) (B‚ÇÇ‚ÇÇ : Matrix m‚ÇÇ n‚ÇÇ R) :
    (fromColumns A‚ÇÅ A‚ÇÇ) * fromBlocks B‚ÇÅ‚ÇÅ B‚ÇÅ‚ÇÇ B‚ÇÇ‚ÇÅ B‚ÇÇ‚ÇÇ =
      fromColumns (A‚ÇÅ * B‚ÇÅ‚ÇÅ + A‚ÇÇ * B‚ÇÇ‚ÇÅ) (A‚ÇÅ * B‚ÇÅ‚ÇÇ + A‚ÇÇ * B‚ÇÇ‚ÇÇ) := by
  ext _ (_ | _) <;> simp [mul_apply]
  -- ‚ä¢ (fromColumns A‚ÇÅ A‚ÇÇ * fromBlocks B‚ÇÅ‚ÇÅ B‚ÇÅ‚ÇÇ B‚ÇÇ‚ÇÅ B‚ÇÇ‚ÇÇ) i‚úù (Sum.inl val‚úù) = fromCol ‚Ä¶
                    -- üéâ no goals
                    -- üéâ no goals

/-- A block matrix mulitplied by a row partitioned matrix gives a row partitioned matrix -/
lemma fromBlocks_mul_fromRows (A‚ÇÅ : Matrix n‚ÇÅ n R) (A‚ÇÇ : Matrix n‚ÇÇ n R)
    (B‚ÇÅ‚ÇÅ : Matrix m‚ÇÅ n‚ÇÅ R) (B‚ÇÅ‚ÇÇ : Matrix m‚ÇÅ n‚ÇÇ R) (B‚ÇÇ‚ÇÅ : Matrix m‚ÇÇ n‚ÇÅ R) (B‚ÇÇ‚ÇÇ : Matrix m‚ÇÇ n‚ÇÇ R) :
    fromBlocks B‚ÇÅ‚ÇÅ B‚ÇÅ‚ÇÇ B‚ÇÇ‚ÇÅ B‚ÇÇ‚ÇÇ * (fromRows A‚ÇÅ A‚ÇÇ) =
      fromRows (B‚ÇÅ‚ÇÅ * A‚ÇÅ + B‚ÇÅ‚ÇÇ * A‚ÇÇ) (B‚ÇÇ‚ÇÅ * A‚ÇÅ + B‚ÇÇ‚ÇÇ * A‚ÇÇ) := by
  ext (_ | _) _ <;> simp [mul_apply]
  -- ‚ä¢ (fromBlocks B‚ÇÅ‚ÇÅ B‚ÇÅ‚ÇÇ B‚ÇÇ‚ÇÅ B‚ÇÇ‚ÇÇ * fromRows A‚ÇÅ A‚ÇÇ) (Sum.inl val‚úù) x‚úù = fromRows ( ‚Ä¶
                    -- üéâ no goals
                    -- üéâ no goals

end Semiring

section CommRing

variable [CommRing R]

/-- Multiplication of a matrix by its inverse is commutative.
This is the column and row partitioned matrix form of `Matrix.mul_eq_one_comm`.

The condition `e : n ‚âÉ n‚ÇÅ ‚äï n‚ÇÇ` states that `fromColumns A‚ÇÅ A‚ÇÇ` and `fromRows B‚ÇÅ B‚ÇÇ` are "square".
-/
lemma fromColumns_mul_fromRows_eq_one_comm (e : n ‚âÉ n‚ÇÅ ‚äï n‚ÇÇ)
    (A‚ÇÅ : Matrix n n‚ÇÅ R) (A‚ÇÇ : Matrix n n‚ÇÇ R) (B‚ÇÅ : Matrix n‚ÇÅ n R) (B‚ÇÇ : Matrix n‚ÇÇ n R) :
    fromColumns A‚ÇÅ A‚ÇÇ * fromRows B‚ÇÅ B‚ÇÇ = 1 ‚Üî fromRows B‚ÇÅ B‚ÇÇ * fromColumns A‚ÇÅ A‚ÇÇ = 1 := by
  calc fromColumns A‚ÇÅ A‚ÇÇ * fromRows B‚ÇÅ B‚ÇÇ = 1
  _ ‚Üî submatrix (fromColumns A‚ÇÅ A‚ÇÇ) id e * submatrix (fromRows B‚ÇÅ B‚ÇÇ) e id = 1 := by
    simp
  _ ‚Üî submatrix (fromRows B‚ÇÅ B‚ÇÇ) e id * submatrix (fromColumns A‚ÇÅ A‚ÇÇ) id e = 1 :=
    mul_eq_one_comm
  _ ‚Üî reindex e.symm e.symm (fromRows B‚ÇÅ B‚ÇÇ * fromColumns A‚ÇÅ A‚ÇÇ) = reindex e.symm e.symm 1 := by
    simp only [reindex_apply, Equiv.symm_symm, submatrix_one_equiv,
        submatrix_mul (he‚ÇÇ := Function.bijective_id)]
  _ ‚Üî fromRows B‚ÇÅ B‚ÇÇ * fromColumns A‚ÇÅ A‚ÇÇ = 1 :=
    (reindex _ _).injective.eq_iff

/- The lemma `fromColumns_mul_fromRows_eq_one_comm` specialized to the case where the index sets n‚ÇÅ
and n‚ÇÇ, are the result of subtyping by a predicate and its complement. -/
lemma equiv_compl_fromColumns_mul_fromRows_eq_one_comm (p : n ‚Üí Prop)[DecidablePred p]
    (A‚ÇÅ : Matrix n {i // p i} R) (A‚ÇÇ : Matrix n {i // ¬¨p i} R)
    (B‚ÇÅ : Matrix {i // p i} n R) (B‚ÇÇ : Matrix {i // ¬¨p i} n R) :
    fromColumns A‚ÇÅ A‚ÇÇ * fromRows B‚ÇÅ B‚ÇÇ = 1 ‚Üî fromRows B‚ÇÅ B‚ÇÇ * fromColumns A‚ÇÅ A‚ÇÇ = 1 :=
  fromColumns_mul_fromRows_eq_one_comm (id (Equiv.sumCompl p).symm) A‚ÇÅ A‚ÇÇ B‚ÇÅ B‚ÇÇ

end CommRing

section Star
variable [Star R]

/- A column partioned matrix in a Star ring when conjugate transposed gives a row partitioned matrix
with the columns of the initial matrix conjugate transposed to become rows. -/
lemma conjTranspose_fromColumns_eq_fromRows_conjTranspose (A‚ÇÅ : Matrix m n‚ÇÅ R)
    (A‚ÇÇ : Matrix m n‚ÇÇ R) :
    conjTranspose (fromColumns A‚ÇÅ A‚ÇÇ) = fromRows (conjTranspose A‚ÇÅ) (conjTranspose A‚ÇÇ) := by
  ext (_ | _) _ <;> simp
  -- ‚ä¢ (fromColumns A‚ÇÅ A‚ÇÇ)·¥¥ (Sum.inl val‚úù) x‚úù = fromRows A‚ÇÅ·¥¥ A‚ÇÇ·¥¥ (Sum.inl val‚úù) x‚úù
                    -- üéâ no goals
                    -- üéâ no goals

/- A row partioned matrix in a Star ring when conjugate transposed gives a column partitioned matrix
with the rows of the initial matrix conjugate transposed to become columns. -/
lemma conjTranspose_fromRows_eq_fromColumns_conjTranspose (A‚ÇÅ : Matrix m‚ÇÅ n R)
    (A‚ÇÇ : Matrix m‚ÇÇ n R) : conjTranspose (fromRows A‚ÇÅ A‚ÇÇ) =
      fromColumns (conjTranspose A‚ÇÅ) (conjTranspose A‚ÇÇ) := by
  ext _ (_ | _) <;> simp
  -- ‚ä¢ (fromRows A‚ÇÅ A‚ÇÇ)·¥¥ i‚úù (Sum.inl val‚úù) = fromColumns A‚ÇÅ·¥¥ A‚ÇÇ·¥¥ i‚úù (Sum.inl val‚úù)
                    -- üéâ no goals
                    -- üéâ no goals

end Star

end Matrix
