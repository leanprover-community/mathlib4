/-
Copyright (c) 2023 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.Algebra.DualNumber
import Mathlib.Data.Matrix.Basic

#align_import data.matrix.dual_number from "leanprover-community/mathlib"@"eb0cb4511aaef0da2462207b67358a0e1fe1e2ee"

/-!
# Matrices of dual numbers are isomorphic to dual numbers over matrices

Showing this for the more general case of `TrivSqZeroExt R M` would require an action between
`Matrix n n R` and `Matrix n n M`, which would risk causing diamonds.
-/


variable {R n : Type} [CommSemiring R] [Fintype n] [DecidableEq n]

open Matrix TrivSqZeroExt

/-- Matrices over dual numbers and dual numbers over matrices are isomorphic. -/
@[simps]
def Matrix.dualNumberEquiv : Matrix n n (DualNumber R) â‰ƒâ‚[R] DualNumber (Matrix n n R) where
  toFun A := âŸ¨of fun i j => (A i j).fst, of fun i j => (A i j).sndâŸ©
  invFun d := of fun i j => (d.fst i j, d.snd i j)
  left_inv A := Matrix.ext fun i j => TrivSqZeroExt.ext rfl rfl
  right_inv d := TrivSqZeroExt.ext (Matrix.ext fun i j => rfl) (Matrix.ext fun i j => rfl)
  map_mul' A B := by
    ext
    -- âŠ¢ fst (Equiv.toFun { toFun := fun A => (â†‘of fun i j => fst (A i j), â†‘of fun i  â€¦
    Â· dsimp [mul_apply]
      -- âŠ¢ fst (Finset.sum Finset.univ fun j => A iâœ j * B j xâœ) = Finset.sum Finset.un â€¦
      simp_rw [fst_sum]
      -- âŠ¢ (Finset.sum Finset.univ fun i => fst (A iâœ i * B i xâœ)) = Finset.sum Finset. â€¦
      rfl
      -- ğŸ‰ no goals
    Â· simp_rw [snd_mul, smul_eq_mul, op_smul_eq_mul]
      -- âŠ¢ snd (â†‘of fun i j => fst ((A * B) i j), â†‘of fun i j => snd ((A * B) i j)) iâœ  â€¦
      simp [mul_apply, snd_sum, snd_mul]
      -- âŠ¢ (Finset.sum Finset.univ fun x => fst (A iâœ x) * snd (B x xâœ) + snd (A iâœ x)  â€¦
      rw [â† Finset.sum_add_distrib]
      -- ğŸ‰ no goals
  map_add' A B := TrivSqZeroExt.ext rfl rfl
  commutes' r := by
    simp_rw [algebraMap_eq_inl', algebraMap_eq_diagonal, Pi.algebraMap_def,
      Algebra.id.map_eq_self, algebraMap_eq_inl, â† diagonal_map (inl_zero R), map_apply, fst_inl,
      snd_inl]
    rfl
    -- ğŸ‰ no goals
#align matrix.dual_number_equiv Matrix.dualNumberEquiv
