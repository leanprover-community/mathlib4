/-
Copyright (c) 2022 Kyle Miller. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kyle Miller
-/
import Mathlib.Logic.Equiv.Basic

#align_import data.finite.defs from "leanprover-community/mathlib"@"a148d797a1094ab554ad4183a4ad6f130358ef64"

/-!
# Definition of the `Finite` typeclass

This file defines a typeclass `Finite` saying that `Î± : Sort*` is finite. A type is `Finite` if it
is equivalent to `Fin n` for some `n`. We also define `Infinite Î±` as a typeclass equivalent to
`Â¬Finite Î±`.

The `Finite` predicate has no computational relevance and, being `Prop`-valued, gets to enjoy proof
irrelevance -- it represents the mere fact that the type is finite.  While the `Finite` class also
represents finiteness of a type, a key difference is that a `Fintype` instance represents finiteness
in a computable way: it gives a concrete algorithm to produce a `Finset` whose elements enumerate
the terms of the given type. As such, one generally relies on congruence lemmas when rewriting
expressions involving `Fintype` instances.

Every `Fintype` instance automatically gives a `Finite` instance, see `Fintype.finite`, but not vice
versa. Every `Fintype` instance should be computable since they are meant for computation. If it's
not possible to write a computable `Fintype` instance, one should prefer writing a `Finite` instance
instead.

## Main definitions

* `Finite Î±` denotes that `Î±` is a finite type.
* `Infinite Î±` denotes that `Î±` is an infinite type.

## Implementation notes

The definition of `Finite Î±` is not just `Nonempty (Fintype Î±)` since `Fintype` requires
that `Î± : Type*`, and the definition in this module allows for `Î± : Sort*`. This means
we can write the instance `Finite.prop`.

## Tags

finite, fintype
-/


universe u v

open Function

variable {Î± Î² : Sort*}

/-- A type is `Finite` if it is in bijective correspondence to some
`Fin n`.

While this could be defined as `Nonempty (Fintype Î±)`, it is defined
in this way to allow there to be `Finite` instances for propositions.
-/
class inductive Finite (Î± : Sort*) : Prop
  | intro {n : â„•} : Î± â‰ƒ Fin n â†’ Finite _
#align finite Finite

theorem finite_iff_exists_equiv_fin {Î± : Sort*} : Finite Î± â†” âˆƒ n, Nonempty (Î± â‰ƒ Fin n) :=
  âŸ¨fun âŸ¨eâŸ© => âŸ¨_, âŸ¨eâŸ©âŸ©, fun âŸ¨_, âŸ¨eâŸ©âŸ© => âŸ¨eâŸ©âŸ©
#align finite_iff_exists_equiv_fin finite_iff_exists_equiv_fin

theorem Finite.exists_equiv_fin (Î± : Sort*) [h : Finite Î±] : âˆƒ n : â„•, Nonempty (Î± â‰ƒ Fin n) :=
  finite_iff_exists_equiv_fin.mp h
#align finite.exists_equiv_fin Finite.exists_equiv_fin

theorem Finite.of_equiv (Î± : Sort*) [h : Finite Î±] (f : Î± â‰ƒ Î²) : Finite Î² := by
  cases' h with n e
  -- âŠ¢ Finite Î²
  exact Finite.intro (f.symm.trans e)
  -- ðŸŽ‰ no goals
#align finite.of_equiv Finite.of_equiv

theorem Equiv.finite_iff (f : Î± â‰ƒ Î²) : Finite Î± â†” Finite Î² :=
  âŸ¨fun _ => Finite.of_equiv _ f, fun _ => Finite.of_equiv _ f.symmâŸ©
#align equiv.finite_iff Equiv.finite_iff

theorem Function.Bijective.finite_iff {f : Î± â†’ Î²} (h : Bijective f) : Finite Î± â†” Finite Î² :=
  (Equiv.ofBijective f h).finite_iff
#align function.bijective.finite_iff Function.Bijective.finite_iff

theorem Finite.ofBijective [Finite Î±] {f : Î± â†’ Î²} (h : Bijective f) : Finite Î² :=
  h.finite_iff.mp â€¹_â€º
#align finite.of_bijective Finite.ofBijective

instance [Finite Î±] : Finite (PLift Î±) :=
  Finite.of_equiv Î± Equiv.plift.symm

instance {Î± : Type v} [Finite Î±] : Finite (ULift.{u} Î±) :=
  Finite.of_equiv Î± Equiv.ulift.symm

/-- A type is said to be infinite if it is not finite. Note that `Infinite Î±` is equivalent to
`IsEmpty (Fintype Î±)` or `IsEmpty (Finite Î±)`. -/
class Infinite (Î± : Sort*) : Prop where
  /-- assertion that `Î±` is `Â¬Finite`-/
  not_finite : Â¬Finite Î±
#align infinite Infinite

@[simp]
theorem not_finite_iff_infinite : Â¬Finite Î± â†” Infinite Î± :=
  âŸ¨Infinite.mk, fun h => h.1âŸ©
#align not_finite_iff_infinite not_finite_iff_infinite

@[simp]
theorem not_infinite_iff_finite : Â¬Infinite Î± â†” Finite Î± :=
  not_finite_iff_infinite.not_right.symm
#align not_infinite_iff_finite not_infinite_iff_finite

theorem Equiv.infinite_iff (e : Î± â‰ƒ Î²) : Infinite Î± â†” Infinite Î² :=
  not_finite_iff_infinite.symm.trans <| e.finite_iff.not.trans not_finite_iff_infinite
#align equiv.infinite_iff Equiv.infinite_iff

instance [Infinite Î±] : Infinite (PLift Î±) :=
  Equiv.plift.infinite_iff.2 â€¹_â€º

instance {Î± : Type v} [Infinite Î±] : Infinite (ULift.{u} Î±) :=
  Equiv.ulift.infinite_iff.2 â€¹_â€º

theorem finite_or_infinite (Î± : Sort*) : Finite Î± âˆ¨ Infinite Î± :=
  or_iff_not_imp_left.2 not_finite_iff_infinite.1
#align finite_or_infinite finite_or_infinite

/-- `Infinite Î±` is not `Finite`-/
theorem not_finite (Î± : Sort*) [Infinite Î±] [Finite Î±] : False :=
  @Infinite.not_finite Î± â€¹_â€º â€¹_â€º
#align not_finite not_finite

protected theorem Finite.false [Infinite Î±] (_ : Finite Î±) : False :=
  not_finite Î±
#align finite.false Finite.false

protected theorem Infinite.false [Finite Î±] (_ : Infinite Î±) : False :=
  @Infinite.not_finite Î± â€¹_â€º â€¹_â€º
#align infinite.false Infinite.false

alias âŸ¨Finite.of_not_infinite, Finite.not_infiniteâŸ© := not_infinite_iff_finite
#align finite.of_not_infinite Finite.of_not_infinite
#align finite.not_infinite Finite.not_infinite
