/-
Copyright (c) 2019 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes
-/
import Mathlib.Order.Basic
import Mathlib.Order.WellFounded
import Mathlib.Algebra.Group.OrderSynonym
import Mathlib.Algebra.Group.Pi
import Mathlib.Algebra.Order.Group.Defs
import Mathlib.Mathport.Notation

#align_import data.pi.lex from "leanprover-community/mathlib"@"6623e6af705e97002a9054c1c05a980180276fc1"

/-!
# Lexicographic order on Pi types

This file defines the lexicographic order for Pi types. `a` is less than `b` if `a i = b i` for all
`i` up to some point `k`, and `a k < b k`.

## Notation

* `Œ†‚Çó i, Œ± i`: Pi type equipped with the lexicographic order. Type synonym of `Œ† i, Œ± i`.

## See also

Related files are:
* `Data.Finset.Colex`: Colexicographic order on finite sets.
* `Data.List.Lex`: Lexicographic order on lists.
* `Data.Sigma.Order`: Lexicographic order on `Œ£‚Çó i, Œ± i`.
* `Data.PSigma.Order`: Lexicographic order on `Œ£‚Çó' i, Œ± i`.
* `Data.Prod.Lex`: Lexicographic order on `Œ± √ó Œ≤`.
-/


variable {Œπ : Type*} {Œ≤ : Œπ ‚Üí Type*} (r : Œπ ‚Üí Œπ ‚Üí Prop) (s : ‚àÄ {i}, Œ≤ i ‚Üí Œ≤ i ‚Üí Prop)

namespace Pi

/-- The lexicographic relation on `Œ† i : Œπ, Œ≤ i`, where `Œπ` is ordered by `r`,
  and each `Œ≤ i` is ordered by `s`. -/
protected def Lex (x y : ‚àÄ i, Œ≤ i) : Prop :=
  ‚àÉ i, (‚àÄ j, r j i ‚Üí x j = y j) ‚àß s (x i) (y i)
#align pi.lex Pi.Lex

/- This unfortunately results in a type that isn't delta-reduced, so we keep the notation out of the
basic API, just in case -/
/-- The notation `Œ†‚Çó i, Œ± i` refers to a pi type equipped with the lexicographic order. -/
notation3 (prettyPrint := false) "Œ†‚Çó "(...)", "r:(scoped p => Lex (‚àÄ i, p i)) => r

@[simp]
theorem toLex_apply (x : ‚àÄ i, Œ≤ i) (i : Œπ) : toLex x i = x i :=
  rfl
#align pi.to_lex_apply Pi.toLex_apply

@[simp]
theorem ofLex_apply (x : Lex (‚àÄ i, Œ≤ i)) (i : Œπ) : ofLex x i = x i :=
  rfl
#align pi.of_lex_apply Pi.ofLex_apply

theorem lex_lt_of_lt_of_preorder [‚àÄ i, Preorder (Œ≤ i)] {r} (hwf : WellFounded r) {x y : ‚àÄ i, Œ≤ i}
    (hlt : x < y) : ‚àÉ i, (‚àÄ j, r j i ‚Üí x j ‚â§ y j ‚àß y j ‚â§ x j) ‚àß x i < y i :=
  let h' := Pi.lt_def.1 hlt
  let ‚ü®i, hi, hl‚ü© := hwf.has_min _ h'.2
  ‚ü®i, fun j hj => ‚ü®h'.1 j, not_not.1 fun h => hl j (lt_of_le_not_le (h'.1 j) h) hj‚ü©, hi‚ü©
#align pi.lex_lt_of_lt_of_preorder Pi.lex_lt_of_lt_of_preorder

theorem lex_lt_of_lt [‚àÄ i, PartialOrder (Œ≤ i)] {r} (hwf : WellFounded r) {x y : ‚àÄ i, Œ≤ i}
    (hlt : x < y) : Pi.Lex r (@fun i => (¬∑ < ¬∑)) x y := by
  simp_rw [Pi.Lex, le_antisymm_iff]
  -- ‚ä¢ ‚àÉ i, (‚àÄ (j : Œπ), r j i ‚Üí x j ‚â§ y j ‚àß y j ‚â§ x j) ‚àß x i < y i
  exact lex_lt_of_lt_of_preorder hwf hlt
  -- üéâ no goals
#align pi.lex_lt_of_lt Pi.lex_lt_of_lt

theorem isTrichotomous_lex [‚àÄ i, IsTrichotomous (Œ≤ i) s] (wf : WellFounded r) :
    IsTrichotomous (‚àÄ i, Œ≤ i) (Pi.Lex r @s) :=
  { trichotomous := fun a b => by
      cases' eq_or_ne a b with hab hab
      -- ‚ä¢ Pi.Lex r s a b ‚à® a = b ‚à® Pi.Lex r s b a
      ¬∑ exact Or.inr (Or.inl hab)
        -- üéâ no goals
      ¬∑ rw [Function.ne_iff] at hab
        -- ‚ä¢ Pi.Lex r s a b ‚à® a = b ‚à® Pi.Lex r s b a
        let i := wf.min _ hab
        -- ‚ä¢ Pi.Lex r s a b ‚à® a = b ‚à® Pi.Lex r s b a
        have hri : ‚àÄ j, r j i ‚Üí a j = b j := by
          intro j
          rw [‚Üê not_imp_not]
          exact fun h' => wf.not_lt_min _ _ h'
        have hne : a i ‚â† b i := wf.min_mem _ hab
        -- ‚ä¢ Pi.Lex r s a b ‚à® a = b ‚à® Pi.Lex r s b a
        cases' trichotomous_of s (a i) (b i) with hi hi
        -- ‚ä¢ Pi.Lex r s a b ‚à® a = b ‚à® Pi.Lex r s b a
        exacts [Or.inl ‚ü®i, hri, hi‚ü©,
          Or.inr <| Or.inr <| ‚ü®i, fun j hj => (hri j hj).symm, hi.resolve_left hne‚ü©] }
#align pi.is_trichotomous_lex Pi.isTrichotomous_lex

instance [LT Œπ] [‚àÄ a, LT (Œ≤ a)] : LT (Lex (‚àÄ i, Œ≤ i)) :=
  ‚ü®Pi.Lex (¬∑ < ¬∑) @fun _ => (¬∑ < ¬∑)‚ü©

instance Lex.isStrictOrder [LinearOrder Œπ] [‚àÄ a, PartialOrder (Œ≤ a)] :
    IsStrictOrder (Lex (‚àÄ i, Œ≤ i)) (¬∑ < ¬∑) where
  irrefl := fun a ‚ü®k, _, hk‚ÇÇ‚ü© => lt_irrefl (a k) hk‚ÇÇ
  trans := by
    rintro a b c ‚ü®N‚ÇÅ, lt_N‚ÇÅ, a_lt_b‚ü© ‚ü®N‚ÇÇ, lt_N‚ÇÇ, b_lt_c‚ü©
    -- ‚ä¢ a < c
    rcases lt_trichotomy N‚ÇÅ N‚ÇÇ with (H | rfl | H)
    exacts [‚ü®N‚ÇÅ, fun j hj => (lt_N‚ÇÅ _ hj).trans (lt_N‚ÇÇ _ <| hj.trans H), lt_N‚ÇÇ _ H ‚ñ∏ a_lt_b‚ü©,
      ‚ü®N‚ÇÅ, fun j hj => (lt_N‚ÇÅ _ hj).trans (lt_N‚ÇÇ _ hj), a_lt_b.trans b_lt_c‚ü©,
      ‚ü®N‚ÇÇ, fun j hj => (lt_N‚ÇÅ _ (hj.trans H)).trans (lt_N‚ÇÇ _ hj), (lt_N‚ÇÅ _ H).symm ‚ñ∏ b_lt_c‚ü©]
#align pi.lex.is_strict_order Pi.Lex.isStrictOrder

instance [LinearOrder Œπ] [‚àÄ a, PartialOrder (Œ≤ a)] : PartialOrder (Lex (‚àÄ i, Œ≤ i)) :=
  partialOrderOfSO (¬∑ < ¬∑)

/-- `Œ†‚Çó i, Œ± i` is a linear order if the original order is well-founded. -/
noncomputable instance [LinearOrder Œπ] [IsWellOrder Œπ (¬∑ < ¬∑)] [‚àÄ a, LinearOrder (Œ≤ a)] :
    LinearOrder (Lex (‚àÄ i, Œ≤ i)) :=
  @linearOrderOfSTO (Œ†‚Çó i, Œ≤ i) (¬∑ < ¬∑)
    { trichotomous := (isTrichotomous_lex _ _ IsWellFounded.wf).1 } (Classical.decRel _)

section PartialOrder

variable [LinearOrder Œπ] [IsWellOrder Œπ (¬∑ < ¬∑)] [‚àÄ i, PartialOrder (Œ≤ i)] {x y : ‚àÄ i, Œ≤ i} {i : Œπ}
  {a : Œ≤ i}

open Function

theorem toLex_monotone : Monotone (@toLex (‚àÄ i, Œ≤ i)) := fun a b h =>
  or_iff_not_imp_left.2 fun hne =>
    let ‚ü®i, hi, hl‚ü© := IsWellFounded.wf.has_min (r := (¬∑ < ¬∑ )) { i | a i ‚â† b i }
      (Function.ne_iff.1 hne)
    ‚ü®i, fun j hj => by
      contrapose! hl
      -- ‚ä¢ ‚àÉ x, x ‚àà {i | a i ‚â† b i} ‚àß x < i
      exact ‚ü®j, hl, hj‚ü©, (h i).lt_of_ne hi‚ü©
      -- üéâ no goals
#align pi.to_lex_monotone Pi.toLex_monotone

theorem toLex_strictMono : StrictMono (@toLex (‚àÄ i, Œ≤ i)) := fun a b h =>
  let ‚ü®i, hi, hl‚ü© := IsWellFounded.wf.has_min (r := (¬∑ < ¬∑ )) { i | a i ‚â† b i }
    (Function.ne_iff.1 h.ne)
  ‚ü®i, fun j hj => by
    contrapose! hl
    -- ‚ä¢ ‚àÉ x, x ‚àà {i | a i ‚â† b i} ‚àß x < i
    exact ‚ü®j, hl, hj‚ü©, (h.le i).lt_of_ne hi‚ü©
    -- üéâ no goals
#align pi.to_lex_strict_mono Pi.toLex_strictMono

@[simp]
theorem lt_toLex_update_self_iff : toLex x < toLex (update x i a) ‚Üî x i < a := by
  refine' ‚ü®_, fun h => toLex_strictMono <| lt_update_self_iff.2 h‚ü©
  -- ‚ä¢ ‚ÜëtoLex x < ‚ÜëtoLex (update x i a) ‚Üí x i < a
  rintro ‚ü®j, hj, h‚ü©
  -- ‚ä¢ x i < a
  dsimp at h
  -- ‚ä¢ x i < a
  obtain rfl : j = i := by
    by_contra H
    rw [update_noteq H] at h
    exact h.false
  rwa [update_same] at h
  -- üéâ no goals
#align pi.lt_to_lex_update_self_iff Pi.lt_toLex_update_self_iff

@[simp]
theorem toLex_update_lt_self_iff : toLex (update x i a) < toLex x ‚Üî a < x i := by
  refine' ‚ü®_, fun h => toLex_strictMono <| update_lt_self_iff.2 h‚ü©
  -- ‚ä¢ ‚ÜëtoLex (update x i a) < ‚ÜëtoLex x ‚Üí a < x i
  rintro ‚ü®j, hj, h‚ü©
  -- ‚ä¢ a < x i
  dsimp at h
  -- ‚ä¢ a < x i
  obtain rfl : j = i := by
    by_contra H
    rw [update_noteq H] at h
    exact h.false
  rwa [update_same] at h
  -- üéâ no goals
#align pi.to_lex_update_lt_self_iff Pi.toLex_update_lt_self_iff

@[simp]
theorem le_toLex_update_self_iff : toLex x ‚â§ toLex (update x i a) ‚Üî x i ‚â§ a := by
  simp_rw [le_iff_lt_or_eq, lt_toLex_update_self_iff, toLex_inj, eq_update_self_iff]
  -- üéâ no goals
#align pi.le_to_lex_update_self_iff Pi.le_toLex_update_self_iff

@[simp]
theorem toLex_update_le_self_iff : toLex (update x i a) ‚â§ toLex x ‚Üî a ‚â§ x i := by
  simp_rw [le_iff_lt_or_eq, toLex_update_lt_self_iff, toLex_inj, update_eq_self_iff]
  -- üéâ no goals
#align pi.to_lex_update_le_self_iff Pi.toLex_update_le_self_iff

end PartialOrder

instance [LinearOrder Œπ] [IsWellOrder Œπ (¬∑ < ¬∑)] [‚àÄ a, PartialOrder (Œ≤ a)] [‚àÄ a, OrderBot (Œ≤ a)] :
    OrderBot (Lex (‚àÄ a, Œ≤ a)) where
  bot := toLex ‚ä•
  bot_le _ := toLex_monotone bot_le

instance [LinearOrder Œπ] [IsWellOrder Œπ (¬∑ < ¬∑)] [‚àÄ a, PartialOrder (Œ≤ a)] [‚àÄ a, OrderTop (Œ≤ a)] :
    OrderTop (Lex (‚àÄ a, Œ≤ a)) where
  top := toLex ‚ä§
  le_top _ := toLex_monotone le_top

instance [LinearOrder Œπ] [IsWellOrder Œπ (¬∑ < ¬∑)] [‚àÄ a, PartialOrder (Œ≤ a)]
    [‚àÄ a, BoundedOrder (Œ≤ a)] : BoundedOrder (Lex (‚àÄ a, Œ≤ a)) :=
  { }

instance [Preorder Œπ] [‚àÄ i, LT (Œ≤ i)] [‚àÄ i, DenselyOrdered (Œ≤ i)] :
    DenselyOrdered (Lex (‚àÄ i, Œ≤ i)) :=
  ‚ü®by
    rintro _ a‚ÇÇ ‚ü®i, h, hi‚ü©
    -- ‚ä¢ ‚àÉ a, a‚ÇÅ‚úù < a ‚àß a < a‚ÇÇ
    obtain ‚ü®a, ha‚ÇÅ, ha‚ÇÇ‚ü© := exists_between hi
    -- ‚ä¢ ‚àÉ a, a‚ÇÅ‚úù < a ‚àß a < a‚ÇÇ
    classical
      refine' ‚ü®Function.update a‚ÇÇ _ a, ‚ü®i, fun j hj => _, _‚ü©, i, fun j hj => _, _‚ü©
      rw [h j hj]
      dsimp only at hj
      ¬∑ rw [Function.update_noteq hj.ne a]
      ¬∑ rwa [Function.update_same i a]
      ¬∑ rw [Function.update_noteq hj.ne a]
      ¬∑ rwa [Function.update_same i a]‚ü©

theorem Lex.noMaxOrder' [Preorder Œπ] [‚àÄ i, LT (Œ≤ i)] (i : Œπ) [NoMaxOrder (Œ≤ i)] :
    NoMaxOrder (Lex (‚àÄ i, Œ≤ i)) :=
  ‚ü®fun a => by
    let ‚ü®b, hb‚ü© := exists_gt (a i)
    -- ‚ä¢ ‚àÉ b, a < b
    classical
    exact ‚ü®Function.update a i b, i, fun j hj =>
      (Function.update_noteq hj.ne b a).symm, by rwa [Function.update_same i b]‚ü©‚ü©
#align pi.lex.no_max_order' Pi.Lex.noMaxOrder'

instance [LinearOrder Œπ] [IsWellOrder Œπ (¬∑ < ¬∑)] [Nonempty Œπ] [‚àÄ i, PartialOrder (Œ≤ i)]
    [‚àÄ i, NoMaxOrder (Œ≤ i)] : NoMaxOrder (Lex (‚àÄ i, Œ≤ i)) :=
  ‚ü®fun a =>
    let ‚ü®_, hb‚ü© := exists_gt (ofLex a)
    ‚ü®_, toLex_strictMono hb‚ü©‚ü©

instance [LinearOrder Œπ] [IsWellOrder Œπ (¬∑ < ¬∑)] [Nonempty Œπ] [‚àÄ i, PartialOrder (Œ≤ i)]
    [‚àÄ i, NoMinOrder (Œ≤ i)] : NoMinOrder (Lex (‚àÄ i, Œ≤ i)) :=
  ‚ü®fun a =>
    let ‚ü®_, hb‚ü© := exists_lt (ofLex a)
    ‚ü®_, toLex_strictMono hb‚ü©‚ü©

section OrderedMonoid

variable [LinearOrder Œπ]

@[to_additive]
instance Lex.orderedCancelCommMonoid [‚àÄ i, OrderedCancelCommMonoid (Œ≤ i)] :
    OrderedCancelCommMonoid (Lex (‚àÄ i, Œ≤ i)) where
  mul_le_mul_left _ _ hxy z :=
    hxy.elim (fun hxyz => hxyz ‚ñ∏ le_rfl) fun ‚ü®i, hi‚ü© =>
      Or.inr ‚ü®i, fun j hji => congr_arg (z j * ¬∑) (hi.1 j hji), mul_lt_mul_left' hi.2 _‚ü©
  le_of_mul_le_mul_left _ _ _ hxyz :=
    hxyz.elim (fun h => (mul_left_cancel h).le) fun ‚ü®i, hi‚ü© =>
      Or.inr ‚ü®i, fun j hj => (mul_left_cancel <| hi.1 j hj), lt_of_mul_lt_mul_left' hi.2‚ü©

@[to_additive]
instance Lex.orderedCommGroup [‚àÄ i, OrderedCommGroup (Œ≤ i)] :
    OrderedCommGroup (Lex (‚àÄ i, Œ≤ i)) where
  mul_le_mul_left _ _ := mul_le_mul_left'
#align pi.lex.ordered_comm_group Pi.Lex.orderedCommGroup
#align pi.lex.ordered_add_comm_group Pi.Lex.orderedAddCommGroup

@[to_additive]
noncomputable instance Lex.linearOrderedCancelCommMonoid [IsWellOrder Œπ (¬∑ < ¬∑)]
    [‚àÄ i, LinearOrderedCancelCommMonoid (Œ≤ i)] :
    LinearOrderedCancelCommMonoid (Lex (‚àÄ i, Œ≤ i)) where
  __ := (inferInstance : LinearOrder (Lex (‚àÄ i, Œ≤ i)))
  __ := (inferInstance: OrderedCancelCommMonoid (Lex (‚àÄ i, Œ≤ i)))

@[to_additive]
noncomputable instance Lex.linearOrderedCommGroup [IsWellOrder Œπ (¬∑ < ¬∑)]
    [‚àÄ i, LinearOrderedCommGroup (Œ≤ i)] :
    LinearOrderedCommGroup (Lex (‚àÄ i, Œ≤ i)) where
  __ := (inferInstance : LinearOrder (Lex (‚àÄ i, Œ≤ i)))
  mul_le_mul_left _ _ := mul_le_mul_left'

end OrderedMonoid

/-- If we swap two strictly decreasing values in a function, then the result is lexicographically
smaller than the original function. -/
theorem lex_desc {Œ±} [Preorder Œπ] [DecidableEq Œπ] [Preorder Œ±] {f : Œπ ‚Üí Œ±} {i j : Œπ} (h‚ÇÅ : i ‚â§ j)
    (h‚ÇÇ : f j < f i) : toLex (f ‚àò Equiv.swap i j) < toLex f :=
  ‚ü®i, fun k hik => congr_arg f (Equiv.swap_apply_of_ne_of_ne hik.ne (hik.trans_le h‚ÇÅ).ne), by
    simpa only [Pi.toLex_apply, Function.comp_apply, Equiv.swap_apply_left] using h‚ÇÇ‚ü©
    -- üéâ no goals
#align pi.lex_desc Pi.lex_desc‚Çì

end Pi
