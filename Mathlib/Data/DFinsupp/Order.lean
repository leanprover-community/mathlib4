/-
Copyright (c) 2021 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Data.DFinsupp.Basic

#align_import data.dfinsupp.order from "leanprover-community/mathlib"@"1d29de43a5ba4662dd33b5cfeecfc2a27a5a8a29"

/-!
# Pointwise order on finitely supported dependent functions

This file lifts order structures on the `Î± i` to `Î â‚€ i, Î± i`.

## Main declarations

* `DFinsupp.orderEmbeddingToFun`: The order embedding from finitely supported dependent functions
  to functions.

-/

open BigOperators

open Finset

variable {Î¹ : Type*} {Î± : Î¹ â†’ Type*}

namespace DFinsupp

/-! ### Order structures -/


section Zero
variable [âˆ€ i, Zero (Î± i)]

section LE

variable [âˆ€ i, LE (Î± i)]

instance : LE (Î â‚€ i, Î± i) :=
  âŸ¨fun f g â†¦ âˆ€ i, f i â‰¤ g iâŸ©

theorem le_def {f g : Î â‚€ i, Î± i} : f â‰¤ g â†” âˆ€ i, f i â‰¤ g i :=
  Iff.rfl
#align dfinsupp.le_def DFinsupp.le_def

/-- The order on `DFinsupp`s over a partial order embeds into the order on functions -/
def orderEmbeddingToFun : (Î â‚€ i, Î± i) â†ªo âˆ€ i, Î± i where
  toFun := FunLike.coe
  inj' := FunLike.coe_injective
  map_rel_iff' := by rfl
                     -- ğŸ‰ no goals
#align dfinsupp.order_embedding_to_fun DFinsupp.orderEmbeddingToFun

-- Porting note: we added implicit arguments here in #3414.
@[simp]
theorem orderEmbeddingToFun_apply {f : Î â‚€ i, Î± i} {i : Î¹} :
    (@orderEmbeddingToFun Î¹ Î± _ _ f) i = f i :=
  rfl
#align dfinsupp.order_embedding_to_fun_apply DFinsupp.orderEmbeddingToFun_apply

end LE

section Preorder

variable [âˆ€ i, Preorder (Î± i)]

instance : Preorder (Î â‚€ i, Î± i) :=
  { (inferInstance : LE (DFinsupp Î±)) with
    le_refl := fun f i â†¦ le_rfl
    le_trans := fun f g h hfg hgh i â†¦ (hfg i).trans (hgh i) }

theorem coeFn_mono : Monotone (FunLike.coe : (Î â‚€ i, Î± i) â†’ âˆ€ i, Î± i) := fun _ _ â†¦ le_def.1
#align dfinsupp.coe_fn_mono DFinsupp.coeFn_mono

end Preorder

instance [âˆ€ i, PartialOrder (Î± i)] : PartialOrder (Î â‚€ i, Î± i) :=
  { (inferInstance : Preorder (DFinsupp Î±)) with
    le_antisymm := fun _ _ hfg hgf â†¦ ext fun i â†¦ (hfg i).antisymm (hgf i) }

instance [âˆ€ i, SemilatticeInf (Î± i)] : SemilatticeInf (Î â‚€ i, Î± i) :=
  { (inferInstance : PartialOrder (DFinsupp Î±)) with
    inf := zipWith (fun _ â†¦ (Â· âŠ“ Â·)) fun _ â†¦ inf_idem
    inf_le_left := fun _ _ _ â†¦ inf_le_left
    inf_le_right := fun _ _ _ â†¦ inf_le_right
    le_inf := fun _ _ _ hf hg i â†¦ le_inf (hf i) (hg i) }

@[simp]
theorem inf_apply [âˆ€ i, SemilatticeInf (Î± i)] (f g : Î â‚€ i, Î± i) (i : Î¹) : (f âŠ“ g) i = f i âŠ“ g i :=
  zipWith_apply _ _ _ _ _
#align dfinsupp.inf_apply DFinsupp.inf_apply

instance [âˆ€ i, SemilatticeSup (Î± i)] : SemilatticeSup (Î â‚€ i, Î± i) :=
  { (inferInstance : PartialOrder (DFinsupp Î±)) with
    sup := zipWith (fun _ â†¦ (Â· âŠ” Â·)) fun _ â†¦ sup_idem
    le_sup_left := fun _ _ _ â†¦ le_sup_left
    le_sup_right := fun _ _ _ â†¦ le_sup_right
    sup_le := fun _ _ _ hf hg i â†¦ sup_le (hf i) (hg i) }

@[simp]
theorem sup_apply [âˆ€ i, SemilatticeSup (Î± i)] (f g : Î â‚€ i, Î± i) (i : Î¹) : (f âŠ” g) i = f i âŠ” g i :=
  zipWith_apply _ _ _ _ _
#align dfinsupp.sup_apply DFinsupp.sup_apply

section Lattice
variable [âˆ€ i, Lattice (Î± i)] (f g : Î â‚€ i, Î± i)

instance lattice : Lattice (Î â‚€ i, Î± i) :=
  { (inferInstance : SemilatticeInf (DFinsupp Î±)),
    (inferInstance : SemilatticeSup (DFinsupp Î±)) with }
#align dfinsupp.lattice DFinsupp.lattice

variable [DecidableEq Î¹] [âˆ€ (i) (x : Î± i), Decidable (x â‰  0)]

theorem support_inf_union_support_sup : (f âŠ“ g).support âˆª (f âŠ” g).support = f.support âˆª g.support :=
  coe_injective $ compl_injective $ by ext; simp [inf_eq_and_sup_eq_iff]
                                       -- âŠ¢ xâœ âˆˆ (â†‘(support (f âŠ“ g) âˆª support (f âŠ” g)))á¶œ â†” xâœ âˆˆ (â†‘(support f âˆª support g â€¦
                                            -- ğŸ‰ no goals
#align dfinsupp.support_inf_union_support_sup DFinsupp.support_inf_union_support_sup

theorem support_sup_union_support_inf : (f âŠ” g).support âˆª (f âŠ“ g).support = f.support âˆª g.support :=
  (union_comm _ _).trans $ support_inf_union_support_sup _ _
#align dfinsupp.support_sup_union_support_inf DFinsupp.support_sup_union_support_inf

end Lattice
end Zero

/-! ### Algebraic order structures -/


instance (Î± : Î¹ â†’ Type*) [âˆ€ i, OrderedAddCommMonoid (Î± i)] : OrderedAddCommMonoid (Î â‚€ i, Î± i) :=
  { (inferInstance : AddCommMonoid (DFinsupp Î±)),
    (inferInstance : PartialOrder (DFinsupp Î±)) with
    add_le_add_left := fun _ _ h c i â†¦ add_le_add_left (h i) (c i) }

instance (Î± : Î¹ â†’ Type*) [âˆ€ i, OrderedCancelAddCommMonoid (Î± i)] :
    OrderedCancelAddCommMonoid (Î â‚€ i, Î± i) :=
  { (inferInstance : OrderedAddCommMonoid (DFinsupp Î±)) with
    le_of_add_le_add_left := fun _ _ _ H i â†¦ le_of_add_le_add_left (H i) }

instance [âˆ€ i, OrderedAddCommMonoid (Î± i)] [âˆ€ i, ContravariantClass (Î± i) (Î± i) (Â· + Â·) (Â· â‰¤ Â·)] :
    ContravariantClass (Î â‚€ i, Î± i) (Î â‚€ i, Î± i) (Â· + Â·) (Â· â‰¤ Â·) :=
  âŸ¨fun _ _ _ H i â†¦ le_of_add_le_add_left (H i)âŸ©

section CanonicallyOrderedAddMonoid

-- porting note: Split into 2 lines to satisfy the unusedVariables linter.
variable (Î±)
variable [âˆ€ i, CanonicallyOrderedAddMonoid (Î± i)]

instance : OrderBot (Î â‚€ i, Î± i) where
  bot := 0
  bot_le := by simp only [le_def, coe_zero, Pi.zero_apply, imp_true_iff, zero_le]
               -- ğŸ‰ no goals

variable {Î±}

protected theorem bot_eq_zero : (âŠ¥ : Î â‚€ i, Î± i) = 0 :=
  rfl
#align dfinsupp.bot_eq_zero DFinsupp.bot_eq_zero

@[simp]
theorem add_eq_zero_iff (f g : Î â‚€ i, Î± i) : f + g = 0 â†” f = 0 âˆ§ g = 0 := by
  simp [FunLike.ext_iff, forall_and]
  -- ğŸ‰ no goals
#align dfinsupp.add_eq_zero_iff DFinsupp.add_eq_zero_iff

section LE

variable [DecidableEq Î¹] [âˆ€ (i) (x : Î± i), Decidable (x â‰  0)] {f g : Î â‚€ i, Î± i} {s : Finset Î¹}

theorem le_iff' (hf : f.support âŠ† s) : f â‰¤ g â†” âˆ€ i âˆˆ s, f i â‰¤ g i :=
  âŸ¨fun h s _ â†¦ h s, fun h s â†¦
    if H : s âˆˆ f.support then h s (hf H) else (not_mem_support_iff.1 H).symm â–¸ zero_le (g s)âŸ©
#align dfinsupp.le_iff' DFinsupp.le_iff'

theorem le_iff : f â‰¤ g â†” âˆ€ i âˆˆ f.support, f i â‰¤ g i :=
  le_iff' <| Subset.refl _
#align dfinsupp.le_iff DFinsupp.le_iff

variable (Î±)

instance decidableLE [âˆ€ i, DecidableRel (@LE.le (Î± i) _)] : DecidableRel (@LE.le (Î â‚€ i, Î± i) _) :=
  fun _ _ â†¦ decidable_of_iff _ le_iff.symm
#align dfinsupp.decidable_le DFinsupp.decidableLE

variable {Î±}

@[simp]
theorem single_le_iff {i : Î¹} {a : Î± i} : single i a â‰¤ f â†” a â‰¤ f i :=
  (le_iff' support_single_subset).trans <| by simp
                                              -- ğŸ‰ no goals
#align dfinsupp.single_le_iff DFinsupp.single_le_iff

end LE

-- porting note: Split into 2 lines to satisfy the unusedVariables linter.
variable (Î±)
variable [âˆ€ i, Sub (Î± i)] [âˆ€ i, OrderedSub (Î± i)] {f g : Î â‚€ i, Î± i} {i : Î¹} {a b : Î± i}

/-- This is called `tsub` for truncated subtraction, to distinguish it with subtraction in an
additive group. -/
instance tsub : Sub (Î â‚€ i, Î± i) :=
  âŸ¨zipWith (fun _ m n â†¦ m - n) fun _ â†¦ tsub_self 0âŸ©
#align dfinsupp.tsub DFinsupp.tsub

variable {Î±}

theorem tsub_apply (f g : Î â‚€ i, Î± i) (i : Î¹) : (f - g) i = f i - g i :=
  zipWith_apply _ _ _ _ _
#align dfinsupp.tsub_apply DFinsupp.tsub_apply

@[simp]
theorem coe_tsub (f g : Î â‚€ i, Î± i) : â‡‘(f - g) = f - g := by
  ext i
  -- âŠ¢ â†‘(f - g) i = (â†‘f - â†‘g) i
  exact tsub_apply f g i
  -- ğŸ‰ no goals
#align dfinsupp.coe_tsub DFinsupp.coe_tsub

variable (Î±)

instance : OrderedSub (Î â‚€ i, Î± i) :=
  âŸ¨fun _ _ _ â†¦ forall_congr' fun _ â†¦ tsub_le_iff_rightâŸ©

instance : CanonicallyOrderedAddMonoid (Î â‚€ i, Î± i) :=
  { (inferInstance : OrderBot (DFinsupp Î±)),
    (inferInstance : OrderedAddCommMonoid (DFinsupp Î±)) with
    exists_add_of_le := by
      intro f g h
      -- âŠ¢ âˆƒ c, g = f + c
      exists g - f
      -- âŠ¢ g = f + (g - f)
      ext i
      -- âŠ¢ â†‘g i = â†‘(f + (g - f)) i
      exact (add_tsub_cancel_of_le <| h i).symm
      -- ğŸ‰ no goals
    le_self_add := fun _ _ _ â†¦ le_self_add }

variable {Î±} [DecidableEq Î¹]

@[simp]
theorem single_tsub : single i (a - b) = single i a - single i b := by
  ext j
  -- âŠ¢ â†‘(single i (a - b)) j = â†‘(single i a - single i b) j
  obtain rfl | h := eq_or_ne i j
  -- âŠ¢ â†‘(single i (a - b)) i = â†‘(single i a - single i b) i
  Â· rw [tsub_apply, single_eq_same, single_eq_same, single_eq_same]
    -- ğŸ‰ no goals
  Â· rw [tsub_apply, single_eq_of_ne h, single_eq_of_ne h, single_eq_of_ne h, tsub_self]
    -- ğŸ‰ no goals
#align dfinsupp.single_tsub DFinsupp.single_tsub

variable [âˆ€ (i) (x : Î± i), Decidable (x â‰  0)]

theorem support_tsub : (f - g).support âŠ† f.support := by
  simp (config := { contextual := true }) only [subset_iff, tsub_eq_zero_iff_le, mem_support_iff,
    Ne.def, coe_tsub, Pi.sub_apply, not_imp_not, zero_le, imp_true_iff]
#align dfinsupp.support_tsub DFinsupp.support_tsub

theorem subset_support_tsub : f.support \ g.support âŠ† (f - g).support := by
  simp (config := { contextual := true }) [subset_iff]
  -- ğŸ‰ no goals
#align dfinsupp.subset_support_tsub DFinsupp.subset_support_tsub

end CanonicallyOrderedAddMonoid

section CanonicallyLinearOrderedAddMonoid

variable [âˆ€ i, CanonicallyLinearOrderedAddMonoid (Î± i)] [DecidableEq Î¹] {f g : Î â‚€ i, Î± i}

@[simp]
theorem support_inf : (f âŠ“ g).support = f.support âˆ© g.support := by
  ext
  -- âŠ¢ aâœ âˆˆ support (f âŠ“ g) â†” aâœ âˆˆ support f âˆ© support g
  simp only [inf_apply, mem_support_iff, Ne.def, Finset.mem_inter]
  -- âŠ¢ Â¬â†‘f aâœ âŠ“ â†‘g aâœ = 0 â†” Â¬â†‘f aâœ = 0 âˆ§ Â¬â†‘g aâœ = 0
  simp only [inf_eq_min, â† nonpos_iff_eq_zero, min_le_iff, not_or]
  -- ğŸ‰ no goals
#align dfinsupp.support_inf DFinsupp.support_inf

@[simp]
theorem support_sup : (f âŠ” g).support = f.support âˆª g.support := by
  ext
  -- âŠ¢ aâœ âˆˆ support (f âŠ” g) â†” aâœ âˆˆ support f âˆª support g
  simp only [Finset.mem_union, mem_support_iff, sup_apply, Ne.def, â† bot_eq_zero]
  -- âŠ¢ Â¬â†‘f aâœ âŠ” â†‘g aâœ = âŠ¥ â†” Â¬â†‘f aâœ = âŠ¥ âˆ¨ Â¬â†‘g aâœ = âŠ¥
  rw [_root_.sup_eq_bot_iff, not_and_or]
  -- ğŸ‰ no goals
#align dfinsupp.support_sup DFinsupp.support_sup

nonrec theorem disjoint_iff : Disjoint f g â†” Disjoint f.support g.support := by
  rw [disjoint_iff, disjoint_iff, DFinsupp.bot_eq_zero, â† DFinsupp.support_eq_empty,
    DFinsupp.support_inf]
  rfl
  -- ğŸ‰ no goals
#align dfinsupp.disjoint_iff DFinsupp.disjoint_iff

end CanonicallyLinearOrderedAddMonoid

end DFinsupp
