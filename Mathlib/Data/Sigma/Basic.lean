/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl
-/
import Mathlib.Init.Function
import Std.Tactic.Ext
import Mathlib.Logic.Function.Basic

#align_import data.sigma.basic from "leanprover-community/mathlib"@"a148d797a1094ab554ad4183a4ad6f130358ef64"

/-!
# Sigma types

This file proves basic results about sigma types.

A sigma type is a dependent pair type. Like `Œ± √ó Œ≤` but where the type of the second component
depends on the first component. More precisely, given `Œ≤ : Œπ ‚Üí Type*`, `Sigma Œ≤` is made of stuff
which is of type `Œ≤ i` for some `i : Œπ`, so the sigma type is a disjoint union of types.
For example, the sum type `X ‚äï Y` can be emulated using a sigma type, by taking `Œπ` with
exactly two elements (see `Equiv.sumEquivSigmaBool`).

`Œ£ x, A x` is notation for `Sigma A` (note that this is `\Sigma`, not the sum operator `‚àë`).
`Œ£ x y z ..., A x y z ...` is notation for `Œ£ x, Œ£ y, Œ£ z, ..., A x y z ...`. Here we have
`Œ± : Type*`, `Œ≤ : Œ± ‚Üí Type*`, `Œ≥ : Œ† a : Œ±, Œ≤ a ‚Üí Type*`, ...,
`A : Œ† (a : Œ±) (b : Œ≤ a) (c : Œ≥ a b) ..., Type*` with `x : Œ±` `y : Œ≤ x`, `z : Œ≥ x y`, ...

## Notes

The definition of `Sigma` takes values in `Type*`. This effectively forbids `Prop`- valued sigma
types. To that effect, we have `PSigma`, which takes value in `Sort*` and carries a more
complicated universe signature as a consequence.
-/

section Sigma

variable {Œ± Œ±‚ÇÅ Œ±‚ÇÇ : Type*} {Œ≤ : Œ± ‚Üí Type*} {Œ≤‚ÇÅ : Œ±‚ÇÅ ‚Üí Type*} {Œ≤‚ÇÇ : Œ±‚ÇÇ ‚Üí Type*}

namespace Sigma

instance instInhabitedSigma [Inhabited Œ±] [Inhabited (Œ≤ default)] : Inhabited (Sigma Œ≤) :=
  ‚ü®‚ü®default, default‚ü©‚ü©

instance instDecidableEqSigma [h‚ÇÅ : DecidableEq Œ±] [h‚ÇÇ : ‚àÄ a, DecidableEq (Œ≤ a)] :
    DecidableEq (Sigma Œ≤)
  | ‚ü®a‚ÇÅ, b‚ÇÅ‚ü©, ‚ü®a‚ÇÇ, b‚ÇÇ‚ü© =>
    match a‚ÇÅ, b‚ÇÅ, a‚ÇÇ, b‚ÇÇ, h‚ÇÅ a‚ÇÅ a‚ÇÇ with
    | _, b‚ÇÅ, _, b‚ÇÇ, isTrue (Eq.refl _) =>
      match b‚ÇÅ, b‚ÇÇ, h‚ÇÇ _ b‚ÇÅ b‚ÇÇ with
      | _, _, isTrue (Eq.refl _) => isTrue rfl
      | _, _, isFalse n => isFalse fun h ‚Ü¶ Sigma.noConfusion h fun _ e‚ÇÇ ‚Ü¶ n <| eq_of_heq e‚ÇÇ
    | _, _, _, _, isFalse n => isFalse fun h ‚Ü¶ Sigma.noConfusion h fun e‚ÇÅ _ ‚Ü¶ n e‚ÇÅ

-- sometimes the built-in injectivity support does not work
@[simp] -- @[nolint simpNF]
theorem mk.inj_iff {a‚ÇÅ a‚ÇÇ : Œ±} {b‚ÇÅ : Œ≤ a‚ÇÅ} {b‚ÇÇ : Œ≤ a‚ÇÇ} :
    Sigma.mk a‚ÇÅ b‚ÇÅ = ‚ü®a‚ÇÇ, b‚ÇÇ‚ü© ‚Üî a‚ÇÅ = a‚ÇÇ ‚àß HEq b‚ÇÅ b‚ÇÇ :=
  ‚ü®Œª h => by cases h; exact ‚ü®rfl, heq_of_eq rfl‚ü©, -- in Lean 3 `simp` solved this
             -- ‚ä¢ a‚ÇÅ = a‚ÇÅ ‚àß HEq b‚ÇÅ b‚ÇÅ
                      -- üéâ no goals
   Œª ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© => by subst h‚ÇÅ; rw [eq_of_heq h‚ÇÇ]‚ü©
                    -- ‚ä¢ { fst := a‚ÇÅ, snd := b‚ÇÅ } = { fst := a‚ÇÅ, snd := b‚ÇÇ }
                              -- üéâ no goals
#align sigma.mk.inj_iff Sigma.mk.inj_iff

@[simp]
theorem eta : ‚àÄ x : Œ£a, Œ≤ a, Sigma.mk x.1 x.2 = x
  | ‚ü®_, _‚ü© => rfl
#align sigma.eta Sigma.eta

@[ext]
theorem ext {x‚ÇÄ x‚ÇÅ : Sigma Œ≤} (h‚ÇÄ : x‚ÇÄ.1 = x‚ÇÅ.1) (h‚ÇÅ : HEq x‚ÇÄ.2 x‚ÇÅ.2) : x‚ÇÄ = x‚ÇÅ := by
  cases x‚ÇÄ; cases x‚ÇÅ; cases h‚ÇÄ; cases h‚ÇÅ; rfl
  -- ‚ä¢ { fst := fst‚úù, snd := snd‚úù } = x‚ÇÅ
            -- ‚ä¢ { fst := fst‚úù¬π, snd := snd‚úù¬π } = { fst := fst‚úù, snd := snd‚úù }
                      -- ‚ä¢ { fst := fst‚úù, snd := snd‚úù¬π } = { fst := fst‚úù, snd := snd‚úù }
                                -- ‚ä¢ { fst := fst‚úù, snd := snd‚úù } = { fst := fst‚úù, snd := snd‚úù }
                                          -- üéâ no goals
#align sigma.ext Sigma.ext

theorem ext_iff {x‚ÇÄ x‚ÇÅ : Sigma Œ≤} : x‚ÇÄ = x‚ÇÅ ‚Üî x‚ÇÄ.1 = x‚ÇÅ.1 ‚àß HEq x‚ÇÄ.2 x‚ÇÅ.2 := by
  cases x‚ÇÄ; cases x‚ÇÅ; exact Sigma.mk.inj_iff
  -- ‚ä¢ { fst := fst‚úù, snd := snd‚úù } = x‚ÇÅ ‚Üî { fst := fst‚úù, snd := snd‚úù }.fst = x‚ÇÅ.fs ‚Ä¶
            -- ‚ä¢ { fst := fst‚úù¬π, snd := snd‚úù¬π } = { fst := fst‚úù, snd := snd‚úù } ‚Üî { fst := fst ‚Ä¶
                      -- üéâ no goals
#align sigma.ext_iff Sigma.ext_iff

/-- A version of `Iff.mp Sigma.ext_iff` for functions from a nonempty type to a sigma type. -/
theorem _root_.Function.eq_of_sigmaMk_comp {Œ≥ : Type*} [Nonempty Œ≥]
    {a b : Œ±} {f : Œ≥ ‚Üí Œ≤ a} {g : Œ≥ ‚Üí Œ≤ b} (h : Sigma.mk a ‚àò f = Sigma.mk b ‚àò g) :
    a = b ‚àß HEq f g := by
  rcases ‚ÄπNonempty Œ≥‚Ä∫ with ‚ü®i‚ü©
  -- ‚ä¢ a = b ‚àß HEq f g
  obtain rfl : a = b := congr_arg Sigma.fst (congr_fun h i)
  -- ‚ä¢ a = a ‚àß HEq f g
  simpa [Function.funext_iff] using h
  -- üéâ no goals

/-- A specialized ext lemma for equality of sigma types over an indexed subtype. -/
@[ext]
theorem subtype_ext {Œ≤ : Type*} {p : Œ± ‚Üí Œ≤ ‚Üí Prop} :
    ‚àÄ {x‚ÇÄ x‚ÇÅ : Œ£a, Subtype (p a)}, x‚ÇÄ.fst = x‚ÇÅ.fst ‚Üí (x‚ÇÄ.snd : Œ≤) = x‚ÇÅ.snd ‚Üí x‚ÇÄ = x‚ÇÅ
  | ‚ü®_, _, _‚ü©, ‚ü®_, _, _‚ü©, rfl, rfl => rfl
#align sigma.subtype_ext Sigma.subtype_ext

theorem subtype_ext_iff {Œ≤ : Type*} {p : Œ± ‚Üí Œ≤ ‚Üí Prop} {x‚ÇÄ x‚ÇÅ : Œ£a, Subtype (p a)} :
    x‚ÇÄ = x‚ÇÅ ‚Üî x‚ÇÄ.fst = x‚ÇÅ.fst ‚àß (x‚ÇÄ.snd : Œ≤) = x‚ÇÅ.snd :=
  ‚ü®fun h ‚Ü¶ h ‚ñ∏ ‚ü®rfl, rfl‚ü©, fun ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ subtype_ext h‚ÇÅ h‚ÇÇ‚ü©
#align sigma.subtype_ext_iff Sigma.subtype_ext_iff

@[simp]
theorem ¬´forall¬ª {p : (Œ£a, Œ≤ a) ‚Üí Prop} : (‚àÄ x, p x) ‚Üî ‚àÄ a b, p ‚ü®a, b‚ü© :=
  ‚ü®fun h a b ‚Ü¶ h ‚ü®a, b‚ü©, fun h ‚ü®a, b‚ü© ‚Ü¶ h a b‚ü©
#align sigma.forall Sigma.forall

@[simp]
theorem ¬´exists¬ª {p : (Œ£a, Œ≤ a) ‚Üí Prop} : (‚àÉ x, p x) ‚Üî ‚àÉ a b, p ‚ü®a, b‚ü© :=
  ‚ü®fun ‚ü®‚ü®a, b‚ü©, h‚ü© ‚Ü¶ ‚ü®a, b, h‚ü©, fun ‚ü®a, b, h‚ü© ‚Ü¶ ‚ü®‚ü®a, b‚ü©, h‚ü©‚ü©
#align sigma.exists Sigma.exists

/-- Map the left and right components of a sigma -/
def map (f‚ÇÅ : Œ±‚ÇÅ ‚Üí Œ±‚ÇÇ) (f‚ÇÇ : ‚àÄ a, Œ≤‚ÇÅ a ‚Üí Œ≤‚ÇÇ (f‚ÇÅ a)) (x : Sigma Œ≤‚ÇÅ) : Sigma Œ≤‚ÇÇ :=
  ‚ü®f‚ÇÅ x.1, f‚ÇÇ x.1 x.2‚ü©
#align sigma.map Sigma.map

lemma map_mk (f‚ÇÅ : Œ±‚ÇÅ ‚Üí Œ±‚ÇÇ) (f‚ÇÇ : ‚àÄ a, Œ≤‚ÇÅ a ‚Üí Œ≤‚ÇÇ (f‚ÇÅ a)) (x : Œ±‚ÇÅ) (y : Œ≤‚ÇÅ x) :
    map f‚ÇÅ f‚ÇÇ ‚ü®x, y‚ü© = ‚ü®f‚ÇÅ x, f‚ÇÇ x y‚ü© := rfl
end Sigma

theorem sigma_mk_injective {i : Œ±} : Function.Injective (@Sigma.mk Œ± Œ≤ i)
  | _, _, rfl => rfl
#align sigma_mk_injective sigma_mk_injective

theorem Function.Injective.sigma_map {f‚ÇÅ : Œ±‚ÇÅ ‚Üí Œ±‚ÇÇ} {f‚ÇÇ : ‚àÄ a, Œ≤‚ÇÅ a ‚Üí Œ≤‚ÇÇ (f‚ÇÅ a)}
  (h‚ÇÅ : Function.Injective f‚ÇÅ) (h‚ÇÇ : ‚àÄ a, Function.Injective (f‚ÇÇ a)) :
    Function.Injective (Sigma.map f‚ÇÅ f‚ÇÇ)
  | ‚ü®i, x‚ü©, ‚ü®j, y‚ü©, h => by
    obtain rfl : i = j := h‚ÇÅ (Sigma.mk.inj_iff.mp h).1
    -- ‚ä¢ { fst := i, snd := x } = { fst := i, snd := y }
    obtain rfl : x = y := h‚ÇÇ i (sigma_mk_injective h)
    -- ‚ä¢ { fst := i, snd := x } = { fst := i, snd := x }
    rfl
    -- üéâ no goals
#align function.injective.sigma_map Function.Injective.sigma_map

theorem Function.Injective.of_sigma_map {f‚ÇÅ : Œ±‚ÇÅ ‚Üí Œ±‚ÇÇ} {f‚ÇÇ : ‚àÄ a, Œ≤‚ÇÅ a ‚Üí Œ≤‚ÇÇ (f‚ÇÅ a)}
    (h : Function.Injective (Sigma.map f‚ÇÅ f‚ÇÇ)) (a : Œ±‚ÇÅ) : Function.Injective (f‚ÇÇ a) :=
  fun x y hxy ‚Ü¶
  sigma_mk_injective <| @h ‚ü®a, x‚ü© ‚ü®a, y‚ü© (Sigma.ext rfl (heq_of_eq hxy))
#align function.injective.of_sigma_map Function.Injective.of_sigma_map

theorem Function.Injective.sigma_map_iff {f‚ÇÅ : Œ±‚ÇÅ ‚Üí Œ±‚ÇÇ} {f‚ÇÇ : ‚àÄ a, Œ≤‚ÇÅ a ‚Üí Œ≤‚ÇÇ (f‚ÇÅ a)}
  (h‚ÇÅ : Function.Injective f‚ÇÅ) :
    Function.Injective (Sigma.map f‚ÇÅ f‚ÇÇ) ‚Üî ‚àÄ a, Function.Injective (f‚ÇÇ a) :=
  ‚ü®fun h ‚Ü¶ h.of_sigma_map, h‚ÇÅ.sigma_map‚ü©
#align function.injective.sigma_map_iff Function.Injective.sigma_map_iff

theorem Function.Surjective.sigma_map {f‚ÇÅ : Œ±‚ÇÅ ‚Üí Œ±‚ÇÇ} {f‚ÇÇ : ‚àÄ a, Œ≤‚ÇÅ a ‚Üí Œ≤‚ÇÇ (f‚ÇÅ a)}
  (h‚ÇÅ : Function.Surjective f‚ÇÅ) (h‚ÇÇ : ‚àÄ a, Function.Surjective (f‚ÇÇ a)) :
    Function.Surjective (Sigma.map f‚ÇÅ f‚ÇÇ) := by
  simp only [Function.Surjective, Sigma.forall, h‚ÇÅ.forall]
  -- ‚ä¢ ‚àÄ (x : Œ±‚ÇÅ) (b : Œ≤‚ÇÇ (f‚ÇÅ x)), ‚àÉ a, Sigma.map f‚ÇÅ f‚ÇÇ a = { fst := f‚ÇÅ x, snd := b }
  exact fun i ‚Ü¶ (h‚ÇÇ _).forall.2 fun x ‚Ü¶ ‚ü®‚ü®i, x‚ü©, rfl‚ü©
  -- üéâ no goals
#align function.surjective.sigma_map Function.Surjective.sigma_map

/-- Interpret a function on `Œ£ x : Œ±, Œ≤ x` as a dependent function with two arguments.

This also exists as an `Equiv` as `Equiv.piCurry Œ≥`. -/
def Sigma.curry {Œ≥ : ‚àÄ a, Œ≤ a ‚Üí Type*} (f : ‚àÄ x : Sigma Œ≤, Œ≥ x.1 x.2) (x : Œ±) (y : Œ≤ x) : Œ≥ x y :=
  f ‚ü®x, y‚ü©
#align sigma.curry Sigma.curry

/-- Interpret a dependent function with two arguments as a function on `Œ£ x : Œ±, Œ≤ x`.

This also exists as an `Equiv` as `(Equiv.piCurry Œ≥).symm`. -/
def Sigma.uncurry {Œ≥ : ‚àÄ a, Œ≤ a ‚Üí Type*} (f : ‚àÄ (x) (y : Œ≤ x), Œ≥ x y) (x : Sigma Œ≤) : Œ≥ x.1 x.2 :=
  f x.1 x.2
#align sigma.uncurry Sigma.uncurry

@[simp]
theorem Sigma.uncurry_curry {Œ≥ : ‚àÄ a, Œ≤ a ‚Üí Type*} (f : ‚àÄ x : Sigma Œ≤, Œ≥ x.1 x.2) :
    Sigma.uncurry (Sigma.curry f) = f :=
  funext fun ‚ü®_, _‚ü© ‚Ü¶ rfl
#align sigma.uncurry_curry Sigma.uncurry_curry

@[simp]
theorem Sigma.curry_uncurry {Œ≥ : ‚àÄ a, Œ≤ a ‚Üí Type*} (f : ‚àÄ (x) (y : Œ≤ x), Œ≥ x y) :
    Sigma.curry (Sigma.uncurry f) = f :=
  rfl
#align sigma.curry_uncurry Sigma.curry_uncurry

/-- Convert a product type to a Œ£-type. -/
def Prod.toSigma {Œ± Œ≤} (p : Œ± √ó Œ≤) : Œ£_ : Œ±, Œ≤ :=
  ‚ü®p.1, p.2‚ü©
#align prod.to_sigma Prod.toSigma

@[simp]
theorem Prod.fst_comp_toSigma {Œ± Œ≤} : Sigma.fst ‚àò @Prod.toSigma Œ± Œ≤ = Prod.fst :=
  rfl
#align prod.fst_comp_to_sigma Prod.fst_comp_toSigma

@[simp]
theorem Prod.fst_toSigma {Œ± Œ≤} (x : Œ± √ó Œ≤) : (Prod.toSigma x).fst = x.fst :=
  rfl
#align prod.fst_to_sigma Prod.fst_toSigma

@[simp]
theorem Prod.snd_toSigma {Œ± Œ≤} (x : Œ± √ó Œ≤) : (Prod.toSigma x).snd = x.snd :=
  rfl
#align prod.snd_to_sigma Prod.snd_toSigma

@[simp]
theorem Prod.toSigma_mk {Œ± Œ≤} (x : Œ±) (y : Œ≤) : (x, y).toSigma = ‚ü®x, y‚ü© :=
  rfl
#align prod.to_sigma_mk Prod.toSigma_mk

-- Porting note: the meta instance `has_reflect (Œ£a, Œ≤ a)` was removed here.

end Sigma

namespace PSigma

variable {Œ± : Sort*} {Œ≤ : Œ± ‚Üí Sort*}

/-- Nondependent eliminator for `PSigma`. -/
def elim {Œ≥} (f : ‚àÄ a, Œ≤ a ‚Üí Œ≥) (a : PSigma Œ≤) : Œ≥ :=
  PSigma.casesOn a f
#align psigma.elim PSigma.elim

@[simp]
theorem elim_val {Œ≥} (f : ‚àÄ a, Œ≤ a ‚Üí Œ≥) (a b) : PSigma.elim f ‚ü®a, b‚ü© = f a b :=
  rfl
#align psigma.elim_val PSigma.elim_val

instance [Inhabited Œ±] [Inhabited (Œ≤ default)] : Inhabited (PSigma Œ≤) :=
  ‚ü®‚ü®default, default‚ü©‚ü©

instance decidableEq [h‚ÇÅ : DecidableEq Œ±] [h‚ÇÇ : ‚àÄ a, DecidableEq (Œ≤ a)] : DecidableEq (PSigma Œ≤)
  | ‚ü®a‚ÇÅ, b‚ÇÅ‚ü©, ‚ü®a‚ÇÇ, b‚ÇÇ‚ü© =>
    match a‚ÇÅ, b‚ÇÅ, a‚ÇÇ, b‚ÇÇ, h‚ÇÅ a‚ÇÅ a‚ÇÇ with
    | _, b‚ÇÅ, _, b‚ÇÇ, isTrue (Eq.refl _) =>
      match b‚ÇÅ, b‚ÇÇ, h‚ÇÇ _ b‚ÇÅ b‚ÇÇ with
      | _, _, isTrue (Eq.refl _) => isTrue rfl
      | _, _, isFalse n => isFalse fun h ‚Ü¶ PSigma.noConfusion h fun _ e‚ÇÇ ‚Ü¶ n <| eq_of_heq e‚ÇÇ
    | _, _, _, _, isFalse n => isFalse fun h ‚Ü¶ PSigma.noConfusion h fun e‚ÇÅ _ ‚Ü¶ n e‚ÇÅ

-- See https://leanprover.zulipchat.com/#narrow/stream/287929-mathlib4/topic/porting.20data.2Esigma.2Ebasic/near/304855864
-- for an explanation of why this is currently needed. It generates `PSigma.mk.inj`.
-- This could be done elsewhere.
gen_injective_theorems% PSigma

theorem mk.inj_iff {a‚ÇÅ a‚ÇÇ : Œ±} {b‚ÇÅ : Œ≤ a‚ÇÅ} {b‚ÇÇ : Œ≤ a‚ÇÇ} :
    @PSigma.mk Œ± Œ≤ a‚ÇÅ b‚ÇÅ = @PSigma.mk Œ± Œ≤ a‚ÇÇ b‚ÇÇ ‚Üî a‚ÇÅ = a‚ÇÇ ‚àß HEq b‚ÇÅ b‚ÇÇ :=
  (Iff.intro PSigma.mk.inj) fun ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶
    match a‚ÇÅ, a‚ÇÇ, b‚ÇÅ, b‚ÇÇ, h‚ÇÅ, h‚ÇÇ with
    | _, _, _, _, Eq.refl _, HEq.refl _ => rfl
#align psigma.mk.inj_iff PSigma.mk.inj_iff

@[ext]
theorem ext {x‚ÇÄ x‚ÇÅ : PSigma Œ≤} (h‚ÇÄ : x‚ÇÄ.1 = x‚ÇÅ.1) (h‚ÇÅ : HEq x‚ÇÄ.2 x‚ÇÅ.2) : x‚ÇÄ = x‚ÇÅ := by
  cases x‚ÇÄ; cases x‚ÇÅ; cases h‚ÇÄ; cases h‚ÇÅ; rfl
  -- ‚ä¢ { fst := fst‚úù, snd := snd‚úù } = x‚ÇÅ
            -- ‚ä¢ { fst := fst‚úù¬π, snd := snd‚úù¬π } = { fst := fst‚úù, snd := snd‚úù }
                      -- ‚ä¢ { fst := fst‚úù, snd := snd‚úù¬π } = { fst := fst‚úù, snd := snd‚úù }
                                -- ‚ä¢ { fst := fst‚úù, snd := snd‚úù } = { fst := fst‚úù, snd := snd‚úù }
                                          -- üéâ no goals
#align psigma.ext PSigma.ext

theorem ext_iff {x‚ÇÄ x‚ÇÅ : PSigma Œ≤} : x‚ÇÄ = x‚ÇÅ ‚Üî x‚ÇÄ.1 = x‚ÇÅ.1 ‚àß HEq x‚ÇÄ.2 x‚ÇÅ.2 := by
  cases x‚ÇÄ; cases x‚ÇÅ; exact PSigma.mk.inj_iff
  -- ‚ä¢ { fst := fst‚úù, snd := snd‚úù } = x‚ÇÅ ‚Üî { fst := fst‚úù, snd := snd‚úù }.fst = x‚ÇÅ.fs ‚Ä¶
            -- ‚ä¢ { fst := fst‚úù¬π, snd := snd‚úù¬π } = { fst := fst‚úù, snd := snd‚úù } ‚Üî { fst := fst ‚Ä¶
                      -- üéâ no goals
#align psigma.ext_iff PSigma.ext_iff

@[simp]
theorem ¬´forall¬ª {p : (Œ£'a, Œ≤ a) ‚Üí Prop} : (‚àÄ x, p x) ‚Üî ‚àÄ a b, p ‚ü®a, b‚ü© :=
  ‚ü®fun h a b ‚Ü¶ h ‚ü®a, b‚ü©, fun h ‚ü®a, b‚ü© ‚Ü¶ h a b‚ü©
#align psigma.forall PSigma.forall

@[simp]
theorem ¬´exists¬ª {p : (Œ£'a, Œ≤ a) ‚Üí Prop} : (‚àÉ x, p x) ‚Üî ‚àÉ a b, p ‚ü®a, b‚ü© :=
  ‚ü®fun ‚ü®‚ü®a, b‚ü©, h‚ü© ‚Ü¶ ‚ü®a, b, h‚ü©, fun ‚ü®a, b, h‚ü© ‚Ü¶ ‚ü®‚ü®a, b‚ü©, h‚ü©‚ü©
#align psigma.exists PSigma.exists

/-- A specialized ext lemma for equality of `PSigma` types over an indexed subtype. -/
@[ext]
theorem subtype_ext {Œ≤ : Sort*} {p : Œ± ‚Üí Œ≤ ‚Üí Prop} :
    ‚àÄ {x‚ÇÄ x‚ÇÅ : Œ£'a, Subtype (p a)}, x‚ÇÄ.fst = x‚ÇÅ.fst ‚Üí (x‚ÇÄ.snd : Œ≤) = x‚ÇÅ.snd ‚Üí x‚ÇÄ = x‚ÇÅ
  | ‚ü®_, _, _‚ü©, ‚ü®_, _, _‚ü©, rfl, rfl => rfl
#align psigma.subtype_ext PSigma.subtype_ext

theorem subtype_ext_iff {Œ≤ : Sort*} {p : Œ± ‚Üí Œ≤ ‚Üí Prop} {x‚ÇÄ x‚ÇÅ : Œ£'a, Subtype (p a)} :
    x‚ÇÄ = x‚ÇÅ ‚Üî x‚ÇÄ.fst = x‚ÇÅ.fst ‚àß (x‚ÇÄ.snd : Œ≤) = x‚ÇÅ.snd :=
  ‚ü®fun h ‚Ü¶ h ‚ñ∏ ‚ü®rfl, rfl‚ü©, fun ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ subtype_ext h‚ÇÅ h‚ÇÇ‚ü©
#align psigma.subtype_ext_iff PSigma.subtype_ext_iff

variable {Œ±‚ÇÅ : Sort*} {Œ±‚ÇÇ : Sort*} {Œ≤‚ÇÅ : Œ±‚ÇÅ ‚Üí Sort*} {Œ≤‚ÇÇ : Œ±‚ÇÇ ‚Üí Sort*}

/-- Map the left and right components of a sigma -/
def map (f‚ÇÅ : Œ±‚ÇÅ ‚Üí Œ±‚ÇÇ) (f‚ÇÇ : ‚àÄ a, Œ≤‚ÇÅ a ‚Üí Œ≤‚ÇÇ (f‚ÇÅ a)) : PSigma Œ≤‚ÇÅ ‚Üí PSigma Œ≤‚ÇÇ
  | ‚ü®a, b‚ü© => ‚ü®f‚ÇÅ a, f‚ÇÇ a b‚ü©
#align psigma.map PSigma.map

end PSigma
