/-
Copyright (c) 2018 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/
import Mathlib.Algebra.BigOperators.Expect
import Mathlib.Algebra.Order.BigOperators.Ring.Finset
import Mathlib.Algebra.Order.Field.Canonical.Basic
import Mathlib.Algebra.Order.Nonneg.Floor
import Mathlib.Algebra.Ring.Regular
import Mathlib.Data.Real.Pointwise
import Mathlib.Order.ConditionallyCompleteLattice.Group

/-!
# Nonnegative real numbers

In this file we define `NNReal` (notation: `‚Ñù‚â•0`) to be the type of non-negative real numbers,
a.k.a. the interval `[0, ‚àû)`. We also define the following operations and structures on `‚Ñù‚â•0`:

* the order on `‚Ñù‚â•0` is the restriction of the order on `‚Ñù`; these relations define a conditionally
  complete linear order with a bottom element, `ConditionallyCompleteLinearOrderBot`;

* `a + b` and `a * b` are the restrictions of addition and multiplication of real numbers to `‚Ñù‚â•0`;
  these operations together with `0 = ‚ü®0, _‚ü©` and `1 = ‚ü®1, _‚ü©` turn `‚Ñù‚â•0` into a conditionally
  complete linear ordered archimedean commutative semifield; we have no typeclass for this in
  `mathlib` yet, so we define the following instances instead:

  - `LinearOrderedSemiring ‚Ñù‚â•0`;
  - `OrderedCommSemiring ‚Ñù‚â•0`;
  - `CanonicallyOrderedCommSemiring ‚Ñù‚â•0`;
  - `LinearOrderedCommGroupWithZero ‚Ñù‚â•0`;
  - `CanonicallyLinearOrderedAddCommMonoid ‚Ñù‚â•0`;
  - `Archimedean ‚Ñù‚â•0`;
  - `ConditionallyCompleteLinearOrderBot ‚Ñù‚â•0`.

  These instances are derived from corresponding instances about the type `{x : Œ± // 0 ‚â§ x}` in an
  appropriate ordered field/ring/group/monoid `Œ±`, see `Mathlib.Algebra.Order.Nonneg.OrderedRing`.

* `Real.toNNReal x` is defined as `‚ü®max x 0, _‚ü©`, i.e. `‚Üë(Real.toNNReal x) = x` when `0 ‚â§ x` and
  `‚Üë(Real.toNNReal x) = 0` otherwise.

We also define an instance `CanLift ‚Ñù ‚Ñù‚â•0`. This instance can be used by the `lift` tactic to
replace `x : ‚Ñù` and `hx : 0 ‚â§ x` in the proof context with `x : ‚Ñù‚â•0` while replacing all occurrences
of `x` with `‚Üëx`. This tactic also works for a function `f : Œ± ‚Üí ‚Ñù` with a hypothesis
`hf : ‚àÄ x, 0 ‚â§ f x`.

## Notations

This file defines `‚Ñù‚â•0` as a localized notation for `NNReal`.
-/

assert_not_exists Star

open Function
open scoped BigOperators

-- to ensure these instances are computable
/-- Nonnegative real numbers. -/
def NNReal := { r : ‚Ñù // 0 ‚â§ r } deriving
  Zero, One, Semiring, StrictOrderedSemiring, CommMonoidWithZero, CommSemiring,
  SemilatticeInf, SemilatticeSup, DistribLattice, OrderedCommSemiring,
  CanonicallyOrderedCommSemiring, Inhabited

namespace NNReal

scoped notation "‚Ñù‚â•0" => NNReal

noncomputable instance : FloorSemiring ‚Ñù‚â•0 := Nonneg.floorSemiring
instance instDenselyOrdered : DenselyOrdered ‚Ñù‚â•0 := Nonneg.instDenselyOrdered
instance : OrderBot ‚Ñù‚â•0 := inferInstance
instance : Archimedean ‚Ñù‚â•0 := Nonneg.instArchimedean
instance : MulArchimedean ‚Ñù‚â•0 := Nonneg.instMulArchimedean
noncomputable instance : Sub ‚Ñù‚â•0 := Nonneg.sub
noncomputable instance : OrderedSub ‚Ñù‚â•0 := Nonneg.orderedSub

noncomputable instance : CanonicallyLinearOrderedSemifield ‚Ñù‚â•0 :=
  Nonneg.canonicallyLinearOrderedSemifield

/-- Coercion `‚Ñù‚â•0 ‚Üí ‚Ñù`. -/
@[coe] def toReal : ‚Ñù‚â•0 ‚Üí ‚Ñù := Subtype.val

instance : Coe ‚Ñù‚â•0 ‚Ñù := ‚ü®toReal‚ü©

-- Simp lemma to put back `n.val` into the normal form given by the coercion.
@[simp]
theorem val_eq_coe (n : ‚Ñù‚â•0) : n.val = n :=
  rfl

instance canLift : CanLift ‚Ñù ‚Ñù‚â•0 toReal fun r => 0 ‚â§ r :=
  Subtype.canLift _

@[ext] protected theorem eq {n m : ‚Ñù‚â•0} : (n : ‚Ñù) = (m : ‚Ñù) ‚Üí n = m :=
  Subtype.eq

theorem ne_iff {x y : ‚Ñù‚â•0} : (x : ‚Ñù) ‚â† (y : ‚Ñù) ‚Üî x ‚â† y :=
  not_congr <| NNReal.eq_iff.symm

protected theorem ¬´forall¬ª {p : ‚Ñù‚â•0 ‚Üí Prop} :
    (‚àÄ x : ‚Ñù‚â•0, p x) ‚Üî ‚àÄ (x : ‚Ñù) (hx : 0 ‚â§ x), p ‚ü®x, hx‚ü© :=
  Subtype.forall

protected theorem ¬´exists¬ª {p : ‚Ñù‚â•0 ‚Üí Prop} :
    (‚àÉ x : ‚Ñù‚â•0, p x) ‚Üî ‚àÉ (x : ‚Ñù) (hx : 0 ‚â§ x), p ‚ü®x, hx‚ü© :=
  Subtype.exists

/-- Reinterpret a real number `r` as a non-negative real number. Returns `0` if `r < 0`. -/
noncomputable def _root_.Real.toNNReal (r : ‚Ñù) : ‚Ñù‚â•0 :=
  ‚ü®max r 0, le_max_right _ _‚ü©

theorem _root_.Real.coe_toNNReal (r : ‚Ñù) (hr : 0 ‚â§ r) : (Real.toNNReal r : ‚Ñù) = r :=
  max_eq_left hr

theorem _root_.Real.toNNReal_of_nonneg {r : ‚Ñù} (hr : 0 ‚â§ r) : r.toNNReal = ‚ü®r, hr‚ü© := by
  simp_rw [Real.toNNReal, max_eq_left hr]

theorem _root_.Real.le_coe_toNNReal (r : ‚Ñù) : r ‚â§ Real.toNNReal r :=
  le_max_left r 0

@[bound] theorem coe_nonneg (r : ‚Ñù‚â•0) : (0 : ‚Ñù) ‚â§ r := r.2

@[simp, norm_cast] theorem coe_mk (a : ‚Ñù) (ha) : toReal ‚ü®a, ha‚ü© = a := rfl

example : Zero ‚Ñù‚â•0 := by infer_instance

example : One ‚Ñù‚â•0 := by infer_instance

example : Add ‚Ñù‚â•0 := by infer_instance

noncomputable example : Sub ‚Ñù‚â•0 := by infer_instance

example : Mul ‚Ñù‚â•0 := by infer_instance

noncomputable example : Inv ‚Ñù‚â•0 := by infer_instance

noncomputable example : Div ‚Ñù‚â•0 := by infer_instance

example : LE ‚Ñù‚â•0 := by infer_instance

example : Bot ‚Ñù‚â•0 := by infer_instance

example : Inhabited ‚Ñù‚â•0 := by infer_instance

example : Nontrivial ‚Ñù‚â•0 := by infer_instance

protected theorem coe_injective : Injective ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) := Subtype.coe_injective

@[simp, norm_cast] lemma coe_inj {r‚ÇÅ r‚ÇÇ : ‚Ñù‚â•0} : (r‚ÇÅ : ‚Ñù) = r‚ÇÇ ‚Üî r‚ÇÅ = r‚ÇÇ :=
  NNReal.coe_injective.eq_iff

@[deprecated (since := "2024-02-03")] protected alias coe_eq := coe_inj

@[simp, norm_cast] lemma coe_zero : ((0 : ‚Ñù‚â•0) : ‚Ñù) = 0 := rfl

@[simp, norm_cast] lemma coe_one : ((1 : ‚Ñù‚â•0) : ‚Ñù) = 1 := rfl

@[simp] lemma mk_zero : (‚ü®0, le_rfl‚ü© : ‚Ñù‚â•0) = 0 := rfl
@[simp] lemma mk_one : (‚ü®1, zero_le_one‚ü© : ‚Ñù‚â•0) = 1 := rfl

@[simp, norm_cast]
protected theorem coe_add (r‚ÇÅ r‚ÇÇ : ‚Ñù‚â•0) : ((r‚ÇÅ + r‚ÇÇ : ‚Ñù‚â•0) : ‚Ñù) = r‚ÇÅ + r‚ÇÇ :=
  rfl

@[simp, norm_cast]
protected theorem coe_mul (r‚ÇÅ r‚ÇÇ : ‚Ñù‚â•0) : ((r‚ÇÅ * r‚ÇÇ : ‚Ñù‚â•0) : ‚Ñù) = r‚ÇÅ * r‚ÇÇ :=
  rfl

@[simp, norm_cast]
protected theorem coe_inv (r : ‚Ñù‚â•0) : ((r‚Åª¬π : ‚Ñù‚â•0) : ‚Ñù) = (r : ‚Ñù)‚Åª¬π :=
  rfl

@[simp, norm_cast]
protected theorem coe_div (r‚ÇÅ r‚ÇÇ : ‚Ñù‚â•0) : ((r‚ÇÅ / r‚ÇÇ : ‚Ñù‚â•0) : ‚Ñù) = (r‚ÇÅ : ‚Ñù) / r‚ÇÇ :=
  rfl

protected theorem coe_two : ((2 : ‚Ñù‚â•0) : ‚Ñù) = 2 := rfl

@[simp, norm_cast]
protected theorem coe_sub {r‚ÇÅ r‚ÇÇ : ‚Ñù‚â•0} (h : r‚ÇÇ ‚â§ r‚ÇÅ) : ((r‚ÇÅ - r‚ÇÇ : ‚Ñù‚â•0) : ‚Ñù) = ‚Üër‚ÇÅ - ‚Üër‚ÇÇ :=
  max_eq_left <| le_sub_comm.2 <| by simp [show (r‚ÇÇ : ‚Ñù) ‚â§ r‚ÇÅ from h]

variable {r r‚ÇÅ r‚ÇÇ : ‚Ñù‚â•0} {x y : ‚Ñù}

@[simp, norm_cast] lemma coe_eq_zero : (r : ‚Ñù) = 0 ‚Üî r = 0 := by rw [‚Üê coe_zero, coe_inj]

@[simp, norm_cast] lemma coe_eq_one : (r : ‚Ñù) = 1 ‚Üî r = 1 := by rw [‚Üê coe_one, coe_inj]

@[norm_cast] lemma coe_ne_zero : (r : ‚Ñù) ‚â† 0 ‚Üî r ‚â† 0 := coe_eq_zero.not

@[norm_cast] lemma coe_ne_one : (r : ‚Ñù) ‚â† 1 ‚Üî r ‚â† 1 := coe_eq_one.not

example : CommSemiring ‚Ñù‚â•0 := by infer_instance

/-- Coercion `‚Ñù‚â•0 ‚Üí ‚Ñù` as a `RingHom`.

Porting note (#11215): TODO: what if we define `Coe ‚Ñù‚â•0 ‚Ñù` using this function? -/
def toRealHom : ‚Ñù‚â•0 ‚Üí+* ‚Ñù where
  toFun := (‚Üë)
  map_one' := NNReal.coe_one
  map_mul' := NNReal.coe_mul
  map_zero' := NNReal.coe_zero
  map_add' := NNReal.coe_add

@[simp] theorem coe_toRealHom : ‚áëtoRealHom = toReal := rfl

section Actions

/-- A `MulAction` over `‚Ñù` restricts to a `MulAction` over `‚Ñù‚â•0`. -/
instance {M : Type*} [MulAction ‚Ñù M] : MulAction ‚Ñù‚â•0 M :=
  MulAction.compHom M toRealHom.toMonoidHom

theorem smul_def {M : Type*} [MulAction ‚Ñù M] (c : ‚Ñù‚â•0) (x : M) : c ‚Ä¢ x = (c : ‚Ñù) ‚Ä¢ x :=
  rfl

instance {M N : Type*} [MulAction ‚Ñù M] [MulAction ‚Ñù N] [SMul M N] [IsScalarTower ‚Ñù M N] :
    IsScalarTower ‚Ñù‚â•0 M N where smul_assoc r := (smul_assoc (r : ‚Ñù) : _)

instance smulCommClass_left {M N : Type*} [MulAction ‚Ñù N] [SMul M N] [SMulCommClass ‚Ñù M N] :
    SMulCommClass ‚Ñù‚â•0 M N where smul_comm r := (smul_comm (r : ‚Ñù) : _)

instance smulCommClass_right {M N : Type*} [MulAction ‚Ñù N] [SMul M N] [SMulCommClass M ‚Ñù N] :
    SMulCommClass M ‚Ñù‚â•0 N where smul_comm m r := (smul_comm m (r : ‚Ñù) : _)

/-- A `DistribMulAction` over `‚Ñù` restricts to a `DistribMulAction` over `‚Ñù‚â•0`. -/
instance {M : Type*} [AddMonoid M] [DistribMulAction ‚Ñù M] : DistribMulAction ‚Ñù‚â•0 M :=
  DistribMulAction.compHom M toRealHom.toMonoidHom

/-- A `Module` over `‚Ñù` restricts to a `Module` over `‚Ñù‚â•0`. -/
instance {M : Type*} [AddCommMonoid M] [Module ‚Ñù M] : Module ‚Ñù‚â•0 M :=
  Module.compHom M toRealHom

-- Porting note (#11215): TODO: after this line, `‚Üë` uses `Algebra.cast` instead of `toReal`
/-- An `Algebra` over `‚Ñù` restricts to an `Algebra` over `‚Ñù‚â•0`. -/
instance {A : Type*} [Semiring A] [Algebra ‚Ñù A] : Algebra ‚Ñù‚â•0 A where
  smul := (¬∑ ‚Ä¢ ¬∑)
  commutes' r x := by simp [Algebra.commutes]
  smul_def' r x := by simp [‚Üê Algebra.smul_def (r : ‚Ñù) x, smul_def]
  toRingHom := (algebraMap ‚Ñù A).comp (toRealHom : ‚Ñù‚â•0 ‚Üí+* ‚Ñù)

-- verify that the above produces instances we might care about
example : Algebra ‚Ñù‚â•0 ‚Ñù := by infer_instance

example : DistribMulAction ‚Ñù‚â•0À£ ‚Ñù := by infer_instance

end Actions

example : MonoidWithZero ‚Ñù‚â•0 := by infer_instance

example : CommMonoidWithZero ‚Ñù‚â•0 := by infer_instance

noncomputable example : CommGroupWithZero ‚Ñù‚â•0 := by infer_instance

@[simp, norm_cast]
theorem coe_indicator {Œ±} (s : Set Œ±) (f : Œ± ‚Üí ‚Ñù‚â•0) (a : Œ±) :
    ((s.indicator f a : ‚Ñù‚â•0) : ‚Ñù) = s.indicator (fun x => ‚Üë(f x)) a :=
  (toRealHom : ‚Ñù‚â•0 ‚Üí+ ‚Ñù).map_indicator _ _ _

@[simp, norm_cast]
theorem coe_pow (r : ‚Ñù‚â•0) (n : ‚Ñï) : ((r ^ n : ‚Ñù‚â•0) : ‚Ñù) = (r : ‚Ñù) ^ n := rfl

@[simp, norm_cast]
theorem coe_zpow (r : ‚Ñù‚â•0) (n : ‚Ñ§) : ((r ^ n : ‚Ñù‚â•0) : ‚Ñù) = (r : ‚Ñù) ^ n := rfl

@[norm_cast]
theorem coe_list_sum (l : List ‚Ñù‚â•0) : ((l.sum : ‚Ñù‚â•0) : ‚Ñù) = (l.map (‚Üë)).sum :=
  map_list_sum toRealHom l

@[norm_cast]
theorem coe_list_prod (l : List ‚Ñù‚â•0) : ((l.prod : ‚Ñù‚â•0) : ‚Ñù) = (l.map (‚Üë)).prod :=
  map_list_prod toRealHom l

@[norm_cast]
theorem coe_multiset_sum (s : Multiset ‚Ñù‚â•0) : ((s.sum : ‚Ñù‚â•0) : ‚Ñù) = (s.map (‚Üë)).sum :=
  map_multiset_sum toRealHom s

@[norm_cast]
theorem coe_multiset_prod (s : Multiset ‚Ñù‚â•0) : ((s.prod : ‚Ñù‚â•0) : ‚Ñù) = (s.map (‚Üë)).prod :=
  map_multiset_prod toRealHom s

variable {Œπ : Type*} {s : Finset Œπ} {f : Œπ ‚Üí ‚Ñù}

@[simp, norm_cast]
theorem coe_sum (s : Finset Œπ) (f : Œπ ‚Üí ‚Ñù‚â•0) : ‚àë i ‚àà s, f i = ‚àë i ‚àà s, (f i : ‚Ñù) :=
  map_sum toRealHom _ _

@[simp, norm_cast]
lemma coe_expect (s : Finset Œπ) (f : Œπ ‚Üí ‚Ñù‚â•0) : ùîº i ‚àà s, f i = ùîº i ‚àà s, (f i : ‚Ñù) :=
  map_expect toRealHom ..

theorem _root_.Real.toNNReal_sum_of_nonneg (hf : ‚àÄ i ‚àà s, 0 ‚â§ f i) :
    Real.toNNReal (‚àë a ‚àà s, f a) = ‚àë a ‚àà s, Real.toNNReal (f a) := by
  rw [‚Üê coe_inj, NNReal.coe_sum, Real.coe_toNNReal _ (Finset.sum_nonneg hf)]
  exact Finset.sum_congr rfl fun x hxs => by rw [Real.coe_toNNReal _ (hf x hxs)]

@[simp, norm_cast]
theorem coe_prod (s : Finset Œπ) (f : Œπ ‚Üí ‚Ñù‚â•0) : ‚Üë(‚àè a ‚àà s, f a) = ‚àè a ‚àà s, (f a : ‚Ñù) :=
  map_prod toRealHom _ _

theorem _root_.Real.toNNReal_prod_of_nonneg (hf : ‚àÄ a, a ‚àà s ‚Üí 0 ‚â§ f a) :
    Real.toNNReal (‚àè a ‚àà s, f a) = ‚àè a ‚àà s, Real.toNNReal (f a) := by
  rw [‚Üê coe_inj, NNReal.coe_prod, Real.coe_toNNReal _ (Finset.prod_nonneg hf)]
  exact Finset.prod_congr rfl fun x hxs => by rw [Real.coe_toNNReal _ (hf x hxs)]

@[simp, norm_cast] lemma coe_nsmul (r : ‚Ñù‚â•0) (n : ‚Ñï) : ‚Üë(n ‚Ä¢ r) = n ‚Ä¢ (r : ‚Ñù) := rfl
@[simp, norm_cast] lemma coe_nnqsmul (q : ‚Ñö‚â•0) (x : ‚Ñù‚â•0) : ‚Üë(q ‚Ä¢ x) = (q ‚Ä¢ x : ‚Ñù) := rfl

@[simp, norm_cast]
protected theorem coe_natCast (n : ‚Ñï) : (‚Üë(‚Üën : ‚Ñù‚â•0) : ‚Ñù) = n :=
  map_natCast toRealHom n

@[deprecated (since := "2024-04-17")]
alias coe_nat_cast := NNReal.coe_natCast

-- See note [no_index around OfNat.ofNat]
@[simp, norm_cast]
protected theorem coe_ofNat (n : ‚Ñï) [n.AtLeastTwo] :
    (no_index (OfNat.ofNat n : ‚Ñù‚â•0) : ‚Ñù) = OfNat.ofNat n :=
  rfl

@[simp, norm_cast]
protected theorem coe_ofScientific (m : ‚Ñï) (s : Bool) (e : ‚Ñï) :
    ‚Üë(OfScientific.ofScientific m s e : ‚Ñù‚â•0) = (OfScientific.ofScientific m s e : ‚Ñù) :=
  rfl

@[simp, norm_cast]
lemma algebraMap_eq_coe : (algebraMap ‚Ñù‚â•0 ‚Ñù : ‚Ñù‚â•0 ‚Üí ‚Ñù) = (‚Üë) := rfl

noncomputable example : LinearOrder ‚Ñù‚â•0 := by infer_instance

@[simp, norm_cast] lemma coe_le_coe : (r‚ÇÅ : ‚Ñù) ‚â§ r‚ÇÇ ‚Üî r‚ÇÅ ‚â§ r‚ÇÇ := Iff.rfl

@[simp, norm_cast] lemma coe_lt_coe : (r‚ÇÅ : ‚Ñù) < r‚ÇÇ ‚Üî r‚ÇÅ < r‚ÇÇ := Iff.rfl

@[bound] private alias ‚ü®_, Bound.coe_lt_coe_of_lt‚ü© := coe_lt_coe

@[simp, norm_cast] lemma coe_pos : (0 : ‚Ñù) < r ‚Üî 0 < r := Iff.rfl

@[bound] private alias ‚ü®_, Bound.coe_pos_of_pos‚ü© := coe_pos

@[simp, norm_cast] lemma one_le_coe : 1 ‚â§ (r : ‚Ñù) ‚Üî 1 ‚â§ r := by rw [‚Üê coe_le_coe, coe_one]
@[simp, norm_cast] lemma one_lt_coe : 1 < (r : ‚Ñù) ‚Üî 1 < r := by rw [‚Üê coe_lt_coe, coe_one]
@[simp, norm_cast] lemma coe_le_one : (r : ‚Ñù) ‚â§ 1 ‚Üî r ‚â§ 1 := by rw [‚Üê coe_le_coe, coe_one]
@[simp, norm_cast] lemma coe_lt_one : (r : ‚Ñù) < 1 ‚Üî r < 1 := by rw [‚Üê coe_lt_coe, coe_one]

@[mono] lemma coe_mono : Monotone ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) := fun _ _ => NNReal.coe_le_coe.2

/-- Alias for the use of `gcongr` -/
@[gcongr] alias ‚ü®_, GCongr.toReal_le_toReal‚ü© := coe_le_coe

protected theorem _root_.Real.toNNReal_mono : Monotone Real.toNNReal := fun _ _ h =>
  max_le_max h (le_refl 0)

@[simp]
theorem _root_.Real.toNNReal_coe {r : ‚Ñù‚â•0} : Real.toNNReal r = r :=
  NNReal.eq <| max_eq_left r.2

@[simp]
theorem mk_natCast (n : ‚Ñï) : @Eq ‚Ñù‚â•0 (‚ü®(n : ‚Ñù), n.cast_nonneg‚ü© : ‚Ñù‚â•0) n :=
  NNReal.eq (NNReal.coe_natCast n).symm

@[deprecated (since := "2024-04-05")] alias mk_coe_nat := mk_natCast

-- Porting note: place this in the `Real` namespace
@[simp]
theorem toNNReal_coe_nat (n : ‚Ñï) : Real.toNNReal n = n :=
  NNReal.eq <| by simp [Real.coe_toNNReal]

-- See note [no_index around OfNat.ofNat]
@[simp]
theorem _root_.Real.toNNReal_ofNat (n : ‚Ñï) [n.AtLeastTwo] :
    Real.toNNReal (no_index (OfNat.ofNat n)) = OfNat.ofNat n :=
  toNNReal_coe_nat n

/-- `Real.toNNReal` and `NNReal.toReal : ‚Ñù‚â•0 ‚Üí ‚Ñù` form a Galois insertion. -/
noncomputable def gi : GaloisInsertion Real.toNNReal (‚Üë) :=
  GaloisInsertion.monotoneIntro NNReal.coe_mono Real.toNNReal_mono Real.le_coe_toNNReal fun _ =>
    Real.toNNReal_coe

-- note that anything involving the (decidability of the) linear order,
-- will be noncomputable, everything else should not be.
example : OrderBot ‚Ñù‚â•0 := by infer_instance

example : PartialOrder ‚Ñù‚â•0 := by infer_instance

noncomputable example : CanonicallyLinearOrderedAddCommMonoid ‚Ñù‚â•0 := by infer_instance

noncomputable example : LinearOrderedAddCommMonoid ‚Ñù‚â•0 := by infer_instance

example : DistribLattice ‚Ñù‚â•0 := by infer_instance

example : SemilatticeInf ‚Ñù‚â•0 := by infer_instance

example : SemilatticeSup ‚Ñù‚â•0 := by infer_instance

noncomputable example : LinearOrderedSemiring ‚Ñù‚â•0 := by infer_instance

example : OrderedCommSemiring ‚Ñù‚â•0 := by infer_instance

noncomputable example : LinearOrderedCommMonoid ‚Ñù‚â•0 := by infer_instance

noncomputable example : LinearOrderedCommMonoidWithZero ‚Ñù‚â•0 := by infer_instance

noncomputable example : LinearOrderedCommGroupWithZero ‚Ñù‚â•0 := by infer_instance

example : CanonicallyOrderedCommSemiring ‚Ñù‚â•0 := by infer_instance

example : DenselyOrdered ‚Ñù‚â•0 := by infer_instance

example : NoMaxOrder ‚Ñù‚â•0 := by infer_instance

instance instPosSMulStrictMono {Œ±} [Preorder Œ±] [MulAction ‚Ñù Œ±] [PosSMulStrictMono ‚Ñù Œ±] :
    PosSMulStrictMono ‚Ñù‚â•0 Œ± where
  elim _r hr _a‚ÇÅ _a‚ÇÇ ha := (smul_lt_smul_of_pos_left ha (coe_pos.2 hr):)

instance instSMulPosStrictMono {Œ±} [Zero Œ±] [Preorder Œ±] [MulAction ‚Ñù Œ±] [SMulPosStrictMono ‚Ñù Œ±] :
    SMulPosStrictMono ‚Ñù‚â•0 Œ± where
  elim _a ha _r‚ÇÅ _r‚ÇÇ hr := (smul_lt_smul_of_pos_right (coe_lt_coe.2 hr) ha :)

/-- If `a` is a nonnegative real number, then the closed interval `[0, a]` in `‚Ñù` is order
isomorphic to the interval `Set.Iic a`. -/
-- Porting note (#11215): TODO: restore once `simps` supports `‚Ñù‚â•0` @[simps!? apply_coe_coe]
def orderIsoIccZeroCoe (a : ‚Ñù‚â•0) : Set.Icc (0 : ‚Ñù) a ‚âÉo Set.Iic a where
  toEquiv := Equiv.Set.sep (Set.Ici 0) fun x : ‚Ñù => x ‚â§ a
  map_rel_iff' := Iff.rfl

@[simp]
theorem orderIsoIccZeroCoe_apply_coe_coe (a : ‚Ñù‚â•0) (b : Set.Icc (0 : ‚Ñù) a) :
    (orderIsoIccZeroCoe a b : ‚Ñù) = b :=
  rfl

@[simp]
theorem orderIsoIccZeroCoe_symm_apply_coe (a : ‚Ñù‚â•0) (b : Set.Iic a) :
    ((orderIsoIccZeroCoe a).symm b : ‚Ñù) = b :=
  rfl

-- note we need the `@` to make the `Membership.mem` have a sensible type
theorem coe_image {s : Set ‚Ñù‚â•0} :
    (‚Üë) '' s = { x : ‚Ñù | ‚àÉ h : 0 ‚â§ x, @Membership.mem ‚Ñù‚â•0 _ _ s ‚ü®x, h‚ü© } :=
  Subtype.coe_image

theorem bddAbove_coe {s : Set ‚Ñù‚â•0} : BddAbove (((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) '' s) ‚Üî BddAbove s :=
  Iff.intro
    (fun ‚ü®b, hb‚ü© =>
      ‚ü®Real.toNNReal b, fun ‚ü®y, _‚ü© hys =>
        show y ‚â§ max b 0 from le_max_of_le_left <| hb <| Set.mem_image_of_mem _ hys‚ü©)
    fun ‚ü®b, hb‚ü© => ‚ü®b, fun _ ‚ü®_, hx, eq‚ü© => eq ‚ñ∏ hb hx‚ü©

theorem bddBelow_coe (s : Set ‚Ñù‚â•0) : BddBelow (((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) '' s) :=
  ‚ü®0, fun _ ‚ü®q, _, eq‚ü© => eq ‚ñ∏ q.2‚ü©

noncomputable instance : ConditionallyCompleteLinearOrderBot ‚Ñù‚â•0 :=
  Nonneg.conditionallyCompleteLinearOrderBot 0

@[norm_cast]
theorem coe_sSup (s : Set ‚Ñù‚â•0) : (‚Üë(sSup s) : ‚Ñù) = sSup (((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) '' s) := by
  rcases Set.eq_empty_or_nonempty s with rfl|hs
  ¬∑ simp
  by_cases H : BddAbove s
  ¬∑ have A : sSup (Subtype.val '' s) ‚àà Set.Ici 0 := by
      apply Real.sSup_nonneg
      rintro - ‚ü®y, -, rfl‚ü©
      exact y.2
    exact (@subset_sSup_of_within ‚Ñù (Set.Ici (0 : ‚Ñù)) _ _ (_) s hs H A).symm
  ¬∑ simp only [csSup_of_not_bddAbove H, csSup_empty, bot_eq_zero', NNReal.coe_zero]
    apply (Real.sSup_of_not_bddAbove ?_).symm
    contrapose! H
    exact bddAbove_coe.1 H

@[simp, norm_cast] -- Porting note: add `simp`
theorem coe_iSup {Œπ : Sort*} (s : Œπ ‚Üí ‚Ñù‚â•0) : (‚Üë(‚®Ü i, s i) : ‚Ñù) = ‚®Ü i, ‚Üë(s i) := by
  rw [iSup, iSup, coe_sSup, ‚Üê Set.range_comp]; rfl

@[norm_cast]
theorem coe_sInf (s : Set ‚Ñù‚â•0) : (‚Üë(sInf s) : ‚Ñù) = sInf (((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) '' s) := by
  rcases Set.eq_empty_or_nonempty s with rfl|hs
  ¬∑ simp only [Set.image_empty, Real.sInf_empty, coe_eq_zero]
    exact @subset_sInf_emptyset ‚Ñù (Set.Ici (0 : ‚Ñù)) _ _ (_)
  have A : sInf (Subtype.val '' s) ‚àà Set.Ici 0 := by
    apply Real.sInf_nonneg
    rintro - ‚ü®y, -, rfl‚ü©
    exact y.2
  exact (@subset_sInf_of_within ‚Ñù (Set.Ici (0 : ‚Ñù)) _ _ (_) s hs (OrderBot.bddBelow s) A).symm

@[simp]
theorem sInf_empty : sInf (‚àÖ : Set ‚Ñù‚â•0) = 0 := by
  rw [‚Üê coe_eq_zero, coe_sInf, Set.image_empty, Real.sInf_empty]

@[norm_cast]
theorem coe_iInf {Œπ : Sort*} (s : Œπ ‚Üí ‚Ñù‚â•0) : (‚Üë(‚®Ö i, s i) : ‚Ñù) = ‚®Ö i, ‚Üë(s i) := by
  rw [iInf, iInf, coe_sInf, ‚Üê Set.range_comp]; rfl

theorem le_iInf_add_iInf {Œπ Œπ' : Sort*} [Nonempty Œπ] [Nonempty Œπ'] {f : Œπ ‚Üí ‚Ñù‚â•0} {g : Œπ' ‚Üí ‚Ñù‚â•0}
    {a : ‚Ñù‚â•0} (h : ‚àÄ i j, a ‚â§ f i + g j) : a ‚â§ (‚®Ö i, f i) + ‚®Ö j, g j := by
  rw [‚Üê NNReal.coe_le_coe, NNReal.coe_add, coe_iInf, coe_iInf]
  exact le_ciInf_add_ciInf h

example : Archimedean ‚Ñù‚â•0 := by infer_instance

-- Porting note (#11215): TODO: remove?
instance covariant_add : CovariantClass ‚Ñù‚â•0 ‚Ñù‚â•0 (¬∑ + ¬∑) (¬∑ ‚â§ ¬∑) := inferInstance

instance contravariant_add : ContravariantClass ‚Ñù‚â•0 ‚Ñù‚â•0 (¬∑ + ¬∑) (¬∑ < ¬∑) := inferInstance

instance covariant_mul : CovariantClass ‚Ñù‚â•0 ‚Ñù‚â•0 (¬∑ * ¬∑) (¬∑ ‚â§ ¬∑) := inferInstance

-- Porting note (#11215): TODO: delete?
nonrec theorem le_of_forall_pos_le_add {a b : ‚Ñù‚â•0} (h : ‚àÄ Œµ, 0 < Œµ ‚Üí a ‚â§ b + Œµ) : a ‚â§ b :=
  le_of_forall_pos_le_add h

theorem lt_iff_exists_rat_btwn (a b : ‚Ñù‚â•0) :
    a < b ‚Üî ‚àÉ q : ‚Ñö, 0 ‚â§ q ‚àß a < Real.toNNReal q ‚àß Real.toNNReal q < b :=
  Iff.intro
    (fun h : (‚Üëa : ‚Ñù) < (‚Üëb : ‚Ñù) =>
      let ‚ü®q, haq, hqb‚ü© := exists_rat_btwn h
      have : 0 ‚â§ (q : ‚Ñù) := le_trans a.2 <| le_of_lt haq
      ‚ü®q, Rat.cast_nonneg.1 this, by
        simp [Real.coe_toNNReal _ this, NNReal.coe_lt_coe.symm, haq, hqb]‚ü©)
    fun ‚ü®q, _, haq, hqb‚ü© => lt_trans haq hqb

theorem bot_eq_zero : (‚ä• : ‚Ñù‚â•0) = 0 := rfl

theorem mul_sup (a b c : ‚Ñù‚â•0) : a * (b ‚äî c) = a * b ‚äî a * c :=
  mul_max_of_nonneg _ _ <| zero_le a

theorem sup_mul (a b c : ‚Ñù‚â•0) : (a ‚äî b) * c = a * c ‚äî b * c :=
  max_mul_of_nonneg _ _ <| zero_le c

theorem mul_finset_sup {Œ±} (r : ‚Ñù‚â•0) (s : Finset Œ±) (f : Œ± ‚Üí ‚Ñù‚â•0) :
    r * s.sup f = s.sup fun a => r * f a :=
  Finset.comp_sup_eq_sup_comp _ (NNReal.mul_sup r) (mul_zero r)

theorem finset_sup_mul {Œ±} (s : Finset Œ±) (f : Œ± ‚Üí ‚Ñù‚â•0) (r : ‚Ñù‚â•0) :
    s.sup f * r = s.sup fun a => f a * r :=
  Finset.comp_sup_eq_sup_comp (¬∑ * r) (fun x y => NNReal.sup_mul x y r) (zero_mul r)

theorem finset_sup_div {Œ±} {f : Œ± ‚Üí ‚Ñù‚â•0} {s : Finset Œ±} (r : ‚Ñù‚â•0) :
    s.sup f / r = s.sup fun a => f a / r := by simp only [div_eq_inv_mul, mul_finset_sup]

@[simp, norm_cast]
theorem coe_max (x y : ‚Ñù‚â•0) : ((max x y : ‚Ñù‚â•0) : ‚Ñù) = max (x : ‚Ñù) (y : ‚Ñù) :=
  NNReal.coe_mono.map_max

@[simp, norm_cast]
theorem coe_min (x y : ‚Ñù‚â•0) : ((min x y : ‚Ñù‚â•0) : ‚Ñù) = min (x : ‚Ñù) (y : ‚Ñù) :=
  NNReal.coe_mono.map_min

@[simp]
theorem zero_le_coe {q : ‚Ñù‚â•0} : 0 ‚â§ (q : ‚Ñù) :=
  q.2

instance instOrderedSMul {M : Type*} [OrderedAddCommMonoid M] [Module ‚Ñù M] [OrderedSMul ‚Ñù M] :
    OrderedSMul ‚Ñù‚â•0 M where
  smul_lt_smul_of_pos hab hc := (smul_lt_smul_of_pos_left hab (NNReal.coe_pos.2 hc) : _)
  lt_of_smul_lt_smul_of_pos {a b c} hab _ :=
    lt_of_smul_lt_smul_of_nonneg_left (by exact hab) (NNReal.coe_nonneg c)

end NNReal

open NNReal

namespace Real

section ToNNReal

@[simp]
theorem coe_toNNReal' (r : ‚Ñù) : (Real.toNNReal r : ‚Ñù) = max r 0 :=
  rfl

@[simp]
theorem toNNReal_zero : Real.toNNReal 0 = 0 := NNReal.eq <| coe_toNNReal _ le_rfl

@[simp]
theorem toNNReal_one : Real.toNNReal 1 = 1 := NNReal.eq <| coe_toNNReal _ zero_le_one

@[simp]
theorem toNNReal_pos {r : ‚Ñù} : 0 < Real.toNNReal r ‚Üî 0 < r := by
  simp [‚Üê NNReal.coe_lt_coe, lt_irrefl]

@[simp]
theorem toNNReal_eq_zero {r : ‚Ñù} : Real.toNNReal r = 0 ‚Üî r ‚â§ 0 := by
  simpa [-toNNReal_pos] using not_iff_not.2 (@toNNReal_pos r)

theorem toNNReal_of_nonpos {r : ‚Ñù} : r ‚â§ 0 ‚Üí Real.toNNReal r = 0 :=
  toNNReal_eq_zero.2

lemma toNNReal_eq_iff_eq_coe {r : ‚Ñù} {p : ‚Ñù‚â•0} (hp : p ‚â† 0) : r.toNNReal = p ‚Üî r = p :=
  ‚ü®fun h ‚Ü¶ h ‚ñ∏ (coe_toNNReal _ <| not_lt.1 fun hlt ‚Ü¶ hp <| h ‚ñ∏ toNNReal_of_nonpos hlt.le).symm,
    fun h ‚Ü¶ h.symm ‚ñ∏ toNNReal_coe‚ü©

@[simp]
lemma toNNReal_eq_one {r : ‚Ñù} : r.toNNReal = 1 ‚Üî r = 1 := toNNReal_eq_iff_eq_coe one_ne_zero

@[simp]
lemma toNNReal_eq_natCast {r : ‚Ñù} {n : ‚Ñï} (hn : n ‚â† 0) : r.toNNReal = n ‚Üî r = n :=
  mod_cast toNNReal_eq_iff_eq_coe <| Nat.cast_ne_zero.2 hn

@[deprecated (since := "2024-04-17")]
alias toNNReal_eq_nat_cast := toNNReal_eq_natCast

@[simp]
lemma toNNReal_eq_ofNat {r : ‚Ñù} {n : ‚Ñï} [n.AtLeastTwo] :
    r.toNNReal = no_index (OfNat.ofNat n) ‚Üî r = OfNat.ofNat n :=
  toNNReal_eq_natCast (NeZero.ne n)

@[simp]
theorem toNNReal_le_toNNReal_iff {r p : ‚Ñù} (hp : 0 ‚â§ p) :
    toNNReal r ‚â§ toNNReal p ‚Üî r ‚â§ p := by simp [‚Üê NNReal.coe_le_coe, hp]

@[simp]
lemma toNNReal_le_one {r : ‚Ñù} : r.toNNReal ‚â§ 1 ‚Üî r ‚â§ 1 := by
  simpa using toNNReal_le_toNNReal_iff zero_le_one

@[simp]
lemma one_lt_toNNReal {r : ‚Ñù} : 1 < r.toNNReal ‚Üî 1 < r := by
  simpa only [not_le] using toNNReal_le_one.not

@[simp]
lemma toNNReal_le_natCast {r : ‚Ñù} {n : ‚Ñï} : r.toNNReal ‚â§ n ‚Üî r ‚â§ n := by
  simpa using toNNReal_le_toNNReal_iff n.cast_nonneg

@[deprecated (since := "2024-04-17")]
alias toNNReal_le_nat_cast := toNNReal_le_natCast

@[simp]
lemma natCast_lt_toNNReal {r : ‚Ñù} {n : ‚Ñï} : n < r.toNNReal ‚Üî n < r := by
  simpa only [not_le] using toNNReal_le_natCast.not

@[deprecated (since := "2024-04-17")]
alias nat_cast_lt_toNNReal := natCast_lt_toNNReal

@[simp]
lemma toNNReal_le_ofNat {r : ‚Ñù} {n : ‚Ñï} [n.AtLeastTwo] :
    r.toNNReal ‚â§ no_index (OfNat.ofNat n) ‚Üî r ‚â§ n :=
  toNNReal_le_natCast

@[simp]
lemma ofNat_lt_toNNReal {r : ‚Ñù} {n : ‚Ñï} [n.AtLeastTwo] :
    no_index (OfNat.ofNat n) < r.toNNReal ‚Üî n < r :=
  natCast_lt_toNNReal

@[simp]
theorem toNNReal_eq_toNNReal_iff {r p : ‚Ñù} (hr : 0 ‚â§ r) (hp : 0 ‚â§ p) :
    toNNReal r = toNNReal p ‚Üî r = p := by simp [‚Üê coe_inj, coe_toNNReal, hr, hp]

@[simp]
theorem toNNReal_lt_toNNReal_iff' {r p : ‚Ñù} : Real.toNNReal r < Real.toNNReal p ‚Üî r < p ‚àß 0 < p :=
  NNReal.coe_lt_coe.symm.trans max_lt_max_left_iff

theorem toNNReal_lt_toNNReal_iff {r p : ‚Ñù} (h : 0 < p) :
    Real.toNNReal r < Real.toNNReal p ‚Üî r < p :=
  toNNReal_lt_toNNReal_iff'.trans (and_iff_left h)

theorem lt_of_toNNReal_lt {r p : ‚Ñù} (h : r.toNNReal < p.toNNReal) : r < p :=
  (Real.toNNReal_lt_toNNReal_iff <| Real.toNNReal_pos.1 (ne_bot_of_gt h).bot_lt).1 h

theorem toNNReal_lt_toNNReal_iff_of_nonneg {r p : ‚Ñù} (hr : 0 ‚â§ r) :
    Real.toNNReal r < Real.toNNReal p ‚Üî r < p :=
  toNNReal_lt_toNNReal_iff'.trans ‚ü®And.left, fun h => ‚ü®h, lt_of_le_of_lt hr h‚ü©‚ü©

lemma toNNReal_le_toNNReal_iff' {r p : ‚Ñù} : r.toNNReal ‚â§ p.toNNReal ‚Üî r ‚â§ p ‚à® r ‚â§ 0 := by
  simp_rw [‚Üê not_lt, toNNReal_lt_toNNReal_iff', not_and_or]

lemma toNNReal_le_toNNReal_iff_of_pos {r p : ‚Ñù} (hr : 0 < r) : r.toNNReal ‚â§ p.toNNReal ‚Üî r ‚â§ p := by
  simp [toNNReal_le_toNNReal_iff', hr.not_le]

@[simp]
lemma one_le_toNNReal {r : ‚Ñù} : 1 ‚â§ r.toNNReal ‚Üî 1 ‚â§ r := by
  simpa using toNNReal_le_toNNReal_iff_of_pos one_pos

@[simp]
lemma toNNReal_lt_one {r : ‚Ñù} : r.toNNReal < 1 ‚Üî r < 1 := by simp only [‚Üê not_le, one_le_toNNReal]

@[simp]
lemma natCastle_toNNReal' {n : ‚Ñï} {r : ‚Ñù} : ‚Üën ‚â§ r.toNNReal ‚Üî n ‚â§ r ‚à® n = 0 := by
  simpa [n.cast_nonneg.le_iff_eq] using toNNReal_le_toNNReal_iff' (r := n)

@[deprecated (since := "2024-04-17")]
alias nat_cast_le_toNNReal' := natCastle_toNNReal'

@[simp]
lemma toNNReal_lt_natCast' {n : ‚Ñï} {r : ‚Ñù} : r.toNNReal < n ‚Üî r < n ‚àß n ‚â† 0 := by
  simpa [pos_iff_ne_zero] using toNNReal_lt_toNNReal_iff' (r := r) (p := n)

@[deprecated (since := "2024-04-17")]
alias toNNReal_lt_nat_cast' := toNNReal_lt_natCast'

lemma natCast_le_toNNReal {n : ‚Ñï} {r : ‚Ñù} (hn : n ‚â† 0) : ‚Üën ‚â§ r.toNNReal ‚Üî n ‚â§ r := by simp [hn]

@[deprecated (since := "2024-04-17")]
alias nat_cast_le_toNNReal := natCast_le_toNNReal

lemma toNNReal_lt_natCast {r : ‚Ñù} {n : ‚Ñï} (hn : n ‚â† 0) : r.toNNReal < n ‚Üî r < n := by simp [hn]

@[deprecated (since := "2024-04-17")]
alias toNNReal_lt_nat_cast := toNNReal_lt_natCast

@[simp]
lemma toNNReal_lt_ofNat {r : ‚Ñù} {n : ‚Ñï} [n.AtLeastTwo] :
    r.toNNReal < no_index (OfNat.ofNat n) ‚Üî r < OfNat.ofNat n :=
  toNNReal_lt_natCast (NeZero.ne n)

@[simp]
lemma ofNat_le_toNNReal {n : ‚Ñï} {r : ‚Ñù} [n.AtLeastTwo] :
    no_index (OfNat.ofNat n) ‚â§ r.toNNReal ‚Üî OfNat.ofNat n ‚â§ r :=
  natCast_le_toNNReal (NeZero.ne n)

@[simp]
theorem toNNReal_add {r p : ‚Ñù} (hr : 0 ‚â§ r) (hp : 0 ‚â§ p) :
    Real.toNNReal (r + p) = Real.toNNReal r + Real.toNNReal p :=
  NNReal.eq <| by simp [hr, hp, add_nonneg]

theorem toNNReal_add_toNNReal {r p : ‚Ñù} (hr : 0 ‚â§ r) (hp : 0 ‚â§ p) :
    Real.toNNReal r + Real.toNNReal p = Real.toNNReal (r + p) :=
  (Real.toNNReal_add hr hp).symm

theorem toNNReal_le_toNNReal {r p : ‚Ñù} (h : r ‚â§ p) : Real.toNNReal r ‚â§ Real.toNNReal p :=
  Real.toNNReal_mono h

theorem toNNReal_add_le {r p : ‚Ñù} : Real.toNNReal (r + p) ‚â§ Real.toNNReal r + Real.toNNReal p :=
  NNReal.coe_le_coe.1 <| max_le (add_le_add (le_max_left _ _) (le_max_left _ _)) NNReal.zero_le_coe

theorem toNNReal_le_iff_le_coe {r : ‚Ñù} {p : ‚Ñù‚â•0} : toNNReal r ‚â§ p ‚Üî r ‚â§ ‚Üëp :=
  NNReal.gi.gc r p

theorem le_toNNReal_iff_coe_le {r : ‚Ñù‚â•0} {p : ‚Ñù} (hp : 0 ‚â§ p) : r ‚â§ Real.toNNReal p ‚Üî ‚Üër ‚â§ p := by
  rw [‚Üê NNReal.coe_le_coe, Real.coe_toNNReal p hp]

theorem le_toNNReal_iff_coe_le' {r : ‚Ñù‚â•0} {p : ‚Ñù} (hr : 0 < r) : r ‚â§ Real.toNNReal p ‚Üî ‚Üër ‚â§ p :=
  (le_or_lt 0 p).elim le_toNNReal_iff_coe_le fun hp => by
    simp only [(hp.trans_le r.coe_nonneg).not_le, toNNReal_eq_zero.2 hp.le, hr.not_le]

theorem toNNReal_lt_iff_lt_coe {r : ‚Ñù} {p : ‚Ñù‚â•0} (ha : 0 ‚â§ r) : Real.toNNReal r < p ‚Üî r < ‚Üëp := by
  rw [‚Üê NNReal.coe_lt_coe, Real.coe_toNNReal r ha]

theorem lt_toNNReal_iff_coe_lt {r : ‚Ñù‚â•0} {p : ‚Ñù} : r < Real.toNNReal p ‚Üî ‚Üër < p :=
  lt_iff_lt_of_le_iff_le toNNReal_le_iff_le_coe

theorem toNNReal_pow {x : ‚Ñù} (hx : 0 ‚â§ x) (n : ‚Ñï) : (x ^ n).toNNReal = x.toNNReal ^ n := by
  rw [‚Üê coe_inj, NNReal.coe_pow, Real.coe_toNNReal _ (pow_nonneg hx _),
    Real.coe_toNNReal x hx]

theorem toNNReal_mul {p q : ‚Ñù} (hp : 0 ‚â§ p) :
    Real.toNNReal (p * q) = Real.toNNReal p * Real.toNNReal q :=
  NNReal.eq <| by simp [mul_max_of_nonneg, hp]

end ToNNReal

end Real

open Real

namespace NNReal

section Mul

theorem mul_eq_mul_left {a b c : ‚Ñù‚â•0} (h : a ‚â† 0) : a * b = a * c ‚Üî b = c := by
  rw [mul_eq_mul_left_iff, or_iff_left h]

end Mul

section Pow

theorem pow_antitone_exp {a : ‚Ñù‚â•0} (m n : ‚Ñï) (mn : m ‚â§ n) (a1 : a ‚â§ 1) : a ^ n ‚â§ a ^ m :=
  pow_le_pow_of_le_one (zero_le a) a1 mn

nonrec theorem exists_pow_lt_of_lt_one {a b : ‚Ñù‚â•0} (ha : 0 < a) (hb : b < 1) :
    ‚àÉ n : ‚Ñï, b ^ n < a := by
  simpa only [‚Üê coe_pow, NNReal.coe_lt_coe] using
    exists_pow_lt_of_lt_one (NNReal.coe_pos.2 ha) (NNReal.coe_lt_coe.2 hb)

nonrec theorem exists_mem_Ico_zpow {x : ‚Ñù‚â•0} {y : ‚Ñù‚â•0} (hx : x ‚â† 0) (hy : 1 < y) :
    ‚àÉ n : ‚Ñ§, x ‚àà Set.Ico (y ^ n) (y ^ (n + 1)) :=
  exists_mem_Ico_zpow (Œ± := ‚Ñù) hx.bot_lt hy

nonrec theorem exists_mem_Ioc_zpow {x : ‚Ñù‚â•0} {y : ‚Ñù‚â•0} (hx : x ‚â† 0) (hy : 1 < y) :
    ‚àÉ n : ‚Ñ§, x ‚àà Set.Ioc (y ^ n) (y ^ (n + 1)) :=
  exists_mem_Ioc_zpow (Œ± := ‚Ñù) hx.bot_lt hy

end Pow

section Sub

/-!
### Lemmas about subtraction

In this section we provide a few lemmas about subtraction that do not fit well into any other
typeclass. For lemmas about subtraction and addition see lemmas about `OrderedSub` in the file
`Mathlib.Algebra.Order.Sub.Basic`. See also `mul_tsub` and `tsub_mul`.
-/

theorem sub_def {r p : ‚Ñù‚â•0} : r - p = Real.toNNReal (r - p) :=
  rfl

theorem coe_sub_def {r p : ‚Ñù‚â•0} : ‚Üë(r - p) = max (r - p : ‚Ñù) 0 :=
  rfl

example : OrderedSub ‚Ñù‚â•0 := by infer_instance

theorem sub_div (a b c : ‚Ñù‚â•0) : (a - b) / c = a / c - b / c :=
  tsub_div _ _ _

end Sub

section Inv

@[simp]
theorem inv_le {r p : ‚Ñù‚â•0} (h : r ‚â† 0) : r‚Åª¬π ‚â§ p ‚Üî 1 ‚â§ r * p := by
  rw [‚Üê mul_le_mul_left (pos_iff_ne_zero.2 h), mul_inv_cancel‚ÇÄ h]

theorem inv_le_of_le_mul {r p : ‚Ñù‚â•0} (h : 1 ‚â§ r * p) : r‚Åª¬π ‚â§ p := by
  by_cases r = 0 <;> simp [*, inv_le]

@[simp]
theorem le_inv_iff_mul_le {r p : ‚Ñù‚â•0} (h : p ‚â† 0) : r ‚â§ p‚Åª¬π ‚Üî r * p ‚â§ 1 := by
  rw [‚Üê mul_le_mul_left (pos_iff_ne_zero.2 h), mul_inv_cancel‚ÇÄ h, mul_comm]

@[simp]
theorem lt_inv_iff_mul_lt {r p : ‚Ñù‚â•0} (h : p ‚â† 0) : r < p‚Åª¬π ‚Üî r * p < 1 := by
  rw [‚Üê mul_lt_mul_left (pos_iff_ne_zero.2 h), mul_inv_cancel‚ÇÄ h, mul_comm]

@[deprecated le_inv_mul_iff‚ÇÄ (since := "2024-08-21")]
theorem mul_le_iff_le_inv {a b r : ‚Ñù‚â•0} (hr : r ‚â† 0) : r * a ‚â§ b ‚Üî a ‚â§ r‚Åª¬π * b :=
  (le_inv_mul_iff‚ÇÄ (pos_iff_ne_zero.2 hr)).symm

@[deprecated le_div_iff‚ÇÄ (since := "2024-08-21")]
theorem le_div_iff_mul_le {a b r : ‚Ñù‚â•0} (hr : r ‚â† 0) : a ‚â§ b / r ‚Üî a * r ‚â§ b :=
  le_div_iff‚ÇÄ (pos_iff_ne_zero.2 hr)

@[deprecated div_le_iff‚ÇÄ (since := "2024-08-21")]
protected lemma div_le_iff {a b r : ‚Ñù‚â•0} (hr : r ‚â† 0) : a / r ‚â§ b ‚Üî a ‚â§ b * r :=
  div_le_iff‚ÇÄ (pos_iff_ne_zero.2 hr)

@[deprecated div_le_iff‚ÇÄ' (since := "2024-08-21")]
protected lemma div_le_iff' {a b r : ‚Ñù‚â•0} (hr : r ‚â† 0) : a / r ‚â§ b ‚Üî a ‚â§ r * b :=
  div_le_iff‚ÇÄ' (pos_iff_ne_zero.2 hr)

theorem div_le_of_le_mul {a b c : ‚Ñù‚â•0} (h : a ‚â§ b * c) : a / c ‚â§ b :=
  if h0 : c = 0 then by simp [h0] else (div_le_iff‚ÇÄ (pos_iff_ne_zero.2 h0)).2 h

theorem div_le_of_le_mul' {a b c : ‚Ñù‚â•0} (h : a ‚â§ b * c) : a / b ‚â§ c :=
  div_le_of_le_mul <| mul_comm b c ‚ñ∏ h

@[deprecated le_div_iff‚ÇÄ (since := "2024-08-21")]
protected lemma le_div_iff {a b r : ‚Ñù‚â•0} (hr : r ‚â† 0) : a ‚â§ b / r ‚Üî a * r ‚â§ b :=
  le_div_iff‚ÇÄ <| pos_iff_ne_zero.2 hr

nonrec theorem le_div_iff' {a b r : ‚Ñù‚â•0} (hr : r ‚â† 0) : a ‚â§ b / r ‚Üî r * a ‚â§ b :=
  le_div_iff‚ÇÄ' <| pos_iff_ne_zero.2 hr

theorem div_lt_iff {a b r : ‚Ñù‚â•0} (hr : r ‚â† 0) : a / r < b ‚Üî a < b * r :=
  lt_iff_lt_of_le_iff_le (le_div_iff‚ÇÄ (pos_iff_ne_zero.2 hr))

theorem div_lt_iff' {a b r : ‚Ñù‚â•0} (hr : r ‚â† 0) : a / r < b ‚Üî a < r * b :=
  lt_iff_lt_of_le_iff_le (le_div_iff‚ÇÄ' (pos_iff_ne_zero.2 hr))

theorem lt_div_iff {a b r : ‚Ñù‚â•0} (hr : r ‚â† 0) : a < b / r ‚Üî a * r < b :=
  lt_iff_lt_of_le_iff_le (div_le_iff‚ÇÄ (pos_iff_ne_zero.2 hr))

theorem lt_div_iff' {a b r : ‚Ñù‚â•0} (hr : r ‚â† 0) : a < b / r ‚Üî r * a < b :=
  lt_iff_lt_of_le_iff_le (div_le_iff‚ÇÄ' (pos_iff_ne_zero.2 hr))

theorem mul_lt_of_lt_div {a b r : ‚Ñù‚â•0} (h : a < b / r) : a * r < b :=
  (lt_div_iff fun hr => False.elim <| by simp [hr] at h).1 h

theorem div_le_div_left_of_le {a b c : ‚Ñù‚â•0} (c0 : c ‚â† 0) (cb : c ‚â§ b) :
    a / b ‚â§ a / c :=
  div_le_div_of_nonneg_left (zero_le _) c0.bot_lt cb

nonrec theorem div_le_div_left {a b c : ‚Ñù‚â•0} (a0 : 0 < a) (b0 : 0 < b) (c0 : 0 < c) :
    a / b ‚â§ a / c ‚Üî c ‚â§ b :=
  div_le_div_left a0 b0 c0

theorem le_of_forall_lt_one_mul_le {x y : ‚Ñù‚â•0} (h : ‚àÄ a < 1, a * x ‚â§ y) : x ‚â§ y :=
  le_of_forall_ge_of_dense fun a ha => by
    have hx : x ‚â† 0 := pos_iff_ne_zero.1 (lt_of_le_of_lt (zero_le _) ha)
    have hx' : x‚Åª¬π ‚â† 0 := by rwa [Ne, inv_eq_zero]
    have : a * x‚Åª¬π < 1 := by rwa [‚Üê lt_inv_iff_mul_lt hx', inv_inv]
    have : a * x‚Åª¬π * x ‚â§ y := h _ this
    rwa [mul_assoc, inv_mul_cancel‚ÇÄ hx, mul_one] at this

nonrec theorem half_le_self (a : ‚Ñù‚â•0) : a / 2 ‚â§ a :=
  half_le_self bot_le

nonrec theorem half_lt_self {a : ‚Ñù‚â•0} (h : a ‚â† 0) : a / 2 < a :=
  half_lt_self h.bot_lt

theorem div_lt_one_of_lt {a b : ‚Ñù‚â•0} (h : a < b) : a / b < 1 := by
  rwa [div_lt_iff, one_mul]
  exact ne_of_gt (lt_of_le_of_lt (zero_le _) h)

theorem _root_.Real.toNNReal_inv {x : ‚Ñù} : Real.toNNReal x‚Åª¬π = (Real.toNNReal x)‚Åª¬π := by
  rcases le_total 0 x with hx | hx
  ¬∑ nth_rw 1 [‚Üê Real.coe_toNNReal x hx]
    rw [‚Üê NNReal.coe_inv, Real.toNNReal_coe]
  ¬∑ rw [toNNReal_eq_zero.mpr hx, inv_zero, toNNReal_eq_zero.mpr (inv_nonpos.mpr hx)]

theorem _root_.Real.toNNReal_div {x y : ‚Ñù} (hx : 0 ‚â§ x) :
    Real.toNNReal (x / y) = Real.toNNReal x / Real.toNNReal y := by
  rw [div_eq_mul_inv, div_eq_mul_inv, ‚Üê Real.toNNReal_inv, ‚Üê Real.toNNReal_mul hx]

theorem _root_.Real.toNNReal_div' {x y : ‚Ñù} (hy : 0 ‚â§ y) :
    Real.toNNReal (x / y) = Real.toNNReal x / Real.toNNReal y := by
  rw [div_eq_inv_mul, div_eq_inv_mul, Real.toNNReal_mul (inv_nonneg.2 hy), Real.toNNReal_inv]

theorem inv_lt_one_iff {x : ‚Ñù‚â•0} (hx : x ‚â† 0) : x‚Åª¬π < 1 ‚Üî 1 < x := by
  rw [‚Üê one_div, div_lt_iff hx, one_mul]

theorem zpow_pos {x : ‚Ñù‚â•0} (hx : x ‚â† 0) (n : ‚Ñ§) : 0 < x ^ n :=
  zpow_pos_of_pos hx.bot_lt _

theorem inv_lt_inv {x y : ‚Ñù‚â•0} (hx : x ‚â† 0) (h : x < y) : y‚Åª¬π < x‚Åª¬π :=
  inv_lt_inv_of_lt hx.bot_lt h

end Inv

@[simp]
theorem abs_eq (x : ‚Ñù‚â•0) : |(x : ‚Ñù)| = x :=
  abs_of_nonneg x.property

section Csupr

open Set

variable {Œπ : Sort*} {f : Œπ ‚Üí ‚Ñù‚â•0}

theorem le_toNNReal_of_coe_le {x : ‚Ñù‚â•0} {y : ‚Ñù} (h : ‚Üëx ‚â§ y) : x ‚â§ y.toNNReal :=
  (le_toNNReal_iff_coe_le <| x.2.trans h).2 h

nonrec theorem sSup_of_not_bddAbove {s : Set ‚Ñù‚â•0} (hs : ¬¨BddAbove s) : SupSet.sSup s = 0 := by
  rw [‚Üê bddAbove_coe] at hs
  rw [‚Üê coe_inj, coe_sSup, NNReal.coe_zero]
  exact sSup_of_not_bddAbove hs

theorem iSup_of_not_bddAbove (hf : ¬¨BddAbove (range f)) : ‚®Ü i, f i = 0 :=
  sSup_of_not_bddAbove hf

theorem iSup_empty [IsEmpty Œπ] (f : Œπ ‚Üí ‚Ñù‚â•0) : ‚®Ü i, f i = 0 := ciSup_of_empty f

theorem iInf_empty [IsEmpty Œπ] (f : Œπ ‚Üí ‚Ñù‚â•0) : ‚®Ö i, f i = 0 := by
  rw [_root_.iInf_of_isEmpty, sInf_empty]

@[simp] lemma iSup_eq_zero (hf : BddAbove (range f)) : ‚®Ü i, f i = 0 ‚Üî ‚àÄ i, f i = 0 := by
  cases isEmpty_or_nonempty Œπ
  ¬∑ simp
  ¬∑ simp [‚Üê bot_eq_zero', ‚Üê le_bot_iff, ciSup_le_iff hf]

@[simp]
theorem iInf_const_zero {Œ± : Sort*} : ‚®Ö _ : Œ±, (0 : ‚Ñù‚â•0) = 0 := by
  rw [‚Üê coe_inj, coe_iInf]
  exact Real.ciInf_const_zero

theorem iInf_mul (f : Œπ ‚Üí ‚Ñù‚â•0) (a : ‚Ñù‚â•0) : iInf f * a = ‚®Ö i, f i * a := by
  rw [‚Üê coe_inj, NNReal.coe_mul, coe_iInf, coe_iInf]
  exact Real.iInf_mul_of_nonneg (NNReal.coe_nonneg _) _

theorem mul_iInf (f : Œπ ‚Üí ‚Ñù‚â•0) (a : ‚Ñù‚â•0) : a * iInf f = ‚®Ö i, a * f i := by
  simpa only [mul_comm] using iInf_mul f a

theorem mul_iSup (f : Œπ ‚Üí ‚Ñù‚â•0) (a : ‚Ñù‚â•0) : (a * ‚®Ü i, f i) = ‚®Ü i, a * f i := by
  rw [‚Üê coe_inj, NNReal.coe_mul, NNReal.coe_iSup, NNReal.coe_iSup]
  exact Real.mul_iSup_of_nonneg (NNReal.coe_nonneg _) _

theorem iSup_mul (f : Œπ ‚Üí ‚Ñù‚â•0) (a : ‚Ñù‚â•0) : (‚®Ü i, f i) * a = ‚®Ü i, f i * a := by
  rw [mul_comm, mul_iSup]
  simp_rw [mul_comm]

theorem iSup_div (f : Œπ ‚Üí ‚Ñù‚â•0) (a : ‚Ñù‚â•0) : (‚®Ü i, f i) / a = ‚®Ü i, f i / a := by
  simp only [div_eq_mul_inv, iSup_mul]

-- Porting note: generalized to allow empty `Œπ`
theorem mul_iSup_le {a : ‚Ñù‚â•0} {g : ‚Ñù‚â•0} {h : Œπ ‚Üí ‚Ñù‚â•0} (H : ‚àÄ j, g * h j ‚â§ a) : g * iSup h ‚â§ a := by
  rw [mul_iSup]
  exact ciSup_le' H

-- Porting note: generalized to allow empty `Œπ`
theorem iSup_mul_le {a : ‚Ñù‚â•0} {g : Œπ ‚Üí ‚Ñù‚â•0} {h : ‚Ñù‚â•0} (H : ‚àÄ i, g i * h ‚â§ a) : iSup g * h ‚â§ a := by
  rw [iSup_mul]
  exact ciSup_le' H

-- Porting note: generalized to allow empty `Œπ`
theorem iSup_mul_iSup_le {a : ‚Ñù‚â•0} {g h : Œπ ‚Üí ‚Ñù‚â•0} (H : ‚àÄ i j, g i * h j ‚â§ a) :
    iSup g * iSup h ‚â§ a :=
  iSup_mul_le fun _ => mul_iSup_le <| H _

variable [Nonempty Œπ]

theorem le_mul_iInf {a : ‚Ñù‚â•0} {g : ‚Ñù‚â•0} {h : Œπ ‚Üí ‚Ñù‚â•0} (H : ‚àÄ j, a ‚â§ g * h j) : a ‚â§ g * iInf h := by
  rw [mul_iInf]
  exact le_ciInf H

theorem le_iInf_mul {a : ‚Ñù‚â•0} {g : Œπ ‚Üí ‚Ñù‚â•0} {h : ‚Ñù‚â•0} (H : ‚àÄ i, a ‚â§ g i * h) : a ‚â§ iInf g * h := by
  rw [iInf_mul]
  exact le_ciInf H

theorem le_iInf_mul_iInf {a : ‚Ñù‚â•0} {g h : Œπ ‚Üí ‚Ñù‚â•0} (H : ‚àÄ i j, a ‚â§ g i * h j) :
    a ‚â§ iInf g * iInf h :=
  le_iInf_mul fun i => le_mul_iInf <| H i

end Csupr

end NNReal

namespace Set

namespace OrdConnected

variable {s : Set ‚Ñù} {t : Set ‚Ñù‚â•0}

theorem preimage_coe_nnreal_real (h : s.OrdConnected) : ((‚Üë) ‚Åª¬π' s : Set ‚Ñù‚â•0).OrdConnected :=
  h.preimage_mono NNReal.coe_mono

theorem image_coe_nnreal_real (h : t.OrdConnected) : ((‚Üë) '' t : Set ‚Ñù).OrdConnected :=
  ‚ü®forall_mem_image.2 fun x hx =>
      forall_mem_image.2 fun _y hy z hz => ‚ü®‚ü®z, x.2.trans hz.1‚ü©, h.out hx hy hz, rfl‚ü©‚ü©

-- Porting note (#11215): TODO: does it generalize to a `GaloisInsertion`?
theorem image_real_toNNReal (h : s.OrdConnected) : (Real.toNNReal '' s).OrdConnected := by
  refine ‚ü®forall_mem_image.2 fun x hx => forall_mem_image.2 fun y hy z hz => ?_‚ü©
  rcases le_total y 0 with hy‚ÇÄ | hy‚ÇÄ
  ¬∑ rw [mem_Icc, Real.toNNReal_of_nonpos hy‚ÇÄ, nonpos_iff_eq_zero] at hz
    exact ‚ü®y, hy, (toNNReal_of_nonpos hy‚ÇÄ).trans hz.2.symm‚ü©
  ¬∑ lift y to ‚Ñù‚â•0 using hy‚ÇÄ
    rw [toNNReal_coe] at hz
    exact ‚ü®z, h.out hx hy ‚ü®toNNReal_le_iff_le_coe.1 hz.1, hz.2‚ü©, toNNReal_coe‚ü©

theorem preimage_real_toNNReal (h : t.OrdConnected) : (Real.toNNReal ‚Åª¬π' t).OrdConnected :=
  h.preimage_mono Real.toNNReal_mono

end OrdConnected

end Set

namespace Real

/-- The absolute value on `‚Ñù` as a map to `‚Ñù‚â•0`. -/
-- Porting note (kmill): `pp_nodot` has no affect here
-- unless RFC lean4#1910 leads to dot notation for CoeFun
@[pp_nodot]
def nnabs : ‚Ñù ‚Üí*‚ÇÄ ‚Ñù‚â•0 where
  toFun x := ‚ü®|x|, abs_nonneg x‚ü©
  map_zero' := by ext; simp
  map_one' := by ext; simp
  map_mul' x y := by ext; simp [abs_mul]

@[norm_cast, simp]
theorem coe_nnabs (x : ‚Ñù) : (nnabs x : ‚Ñù) = |x| :=
  rfl

@[simp]
theorem nnabs_of_nonneg {x : ‚Ñù} (h : 0 ‚â§ x) : nnabs x = toNNReal x := by
  ext
  rw [coe_toNNReal x h, coe_nnabs, abs_of_nonneg h]

theorem nnabs_coe (x : ‚Ñù‚â•0) : nnabs x = x := by simp

theorem coe_toNNReal_le (x : ‚Ñù) : (toNNReal x : ‚Ñù) ‚â§ |x| :=
  max_le (le_abs_self _) (abs_nonneg _)

@[simp] lemma toNNReal_abs (x : ‚Ñù) : |x|.toNNReal = nnabs x := NNReal.coe_injective <| by simp

theorem cast_natAbs_eq_nnabs_cast (n : ‚Ñ§) : (n.natAbs : ‚Ñù‚â•0) = nnabs n := by
  ext
  rw [NNReal.coe_natCast, Int.cast_natAbs, Real.coe_nnabs, Int.cast_abs]

end Real

section StrictMono

open NNReal

variable {Œì‚ÇÄ : Type*} [LinearOrderedCommGroupWithZero Œì‚ÇÄ]

/-- If `Œì‚ÇÄÀ£` is nontrivial and `f : Œì‚ÇÄ ‚Üí*‚ÇÄ ‚Ñù‚â•0` is strictly monotone, then for any positive
  `r : ‚Ñù‚â•0`, there exists `d : Œì‚ÇÄÀ£` with `f d < r`. -/
theorem NNReal.exists_lt_of_strictMono [h : Nontrivial Œì‚ÇÄÀ£] {f : Œì‚ÇÄ ‚Üí*‚ÇÄ ‚Ñù‚â•0} (hf : StrictMono f)
    {r : ‚Ñù‚â•0} (hr : 0 < r) : ‚àÉ d : Œì‚ÇÄÀ£, f d < r := by
  obtain ‚ü®g, hg1‚ü© := (nontrivial_iff_exists_ne (1 : Œì‚ÇÄÀ£)).mp h
  set u : Œì‚ÇÄÀ£ := if g < 1 then g else g‚Åª¬π with hu
  have hfu : f u < 1 := by
    rw [hu]
    split_ifs with hu1
    ¬∑ rw [‚Üê map_one f]; exact hf hu1
    ¬∑ have hfg0 : f g ‚â† 0 :=
        fun h0 ‚Ü¶ (Units.ne_zero g) ((map_eq_zero f).mp h0)
      have hg1' : 1 < g := lt_of_le_of_ne (not_lt.mp hu1) hg1.symm
      rw [Units.val_inv_eq_inv_val, map_inv‚ÇÄ, inv_lt_one_iff hfg0, ‚Üê map_one f]
      exact hf hg1'
  obtain ‚ü®n, hn‚ü© := exists_pow_lt_of_lt_one hr hfu
  use u ^ n
  rwa [Units.val_pow_eq_pow_val, map_pow]

/-- If `Œì‚ÇÄÀ£` is nontrivial and `f : Œì‚ÇÄ ‚Üí*‚ÇÄ ‚Ñù‚â•0` is strictly monotone, then for any positive
  real `r`, there exists `d : Œì‚ÇÄÀ£` with `f d < r`. -/
theorem Real.exists_lt_of_strictMono [h : Nontrivial Œì‚ÇÄÀ£] {f : Œì‚ÇÄ ‚Üí*‚ÇÄ ‚Ñù‚â•0} (hf : StrictMono f)
    {r : ‚Ñù} (hr : 0 < r) : ‚àÉ d : Œì‚ÇÄÀ£, (f d : ‚Ñù) < r := by
  set s : NNReal := ‚ü®r, le_of_lt hr‚ü©
  have hs : 0 < s := hr
  exact NNReal.exists_lt_of_strictMono hf hs

end StrictMono

namespace Mathlib.Meta.Positivity

open Lean Meta Qq Function

private alias ‚ü®_, nnreal_coe_pos‚ü© := coe_pos

/-- Extension for the `positivity` tactic: cast from `‚Ñù‚â•0` to `‚Ñù`. -/
@[positivity NNReal.toReal _]
def evalNNRealtoReal : PositivityExt where eval {u Œ±} _zŒ± _pŒ± e := do
  match u, Œ±, e with
  | 0, ~q(‚Ñù), ~q(NNReal.toReal $a) =>
    let ra ‚Üê core q(inferInstance) q(inferInstance) a
    assertInstancesCommute
    match ra with
    | .positive pa => pure (.positive q(nnreal_coe_pos $pa))
    | _ => pure (.nonnegative q(NNReal.coe_nonneg $a))
  | _, _, _ => throwError "not NNReal.toReal"

end Mathlib.Meta.Positivity
