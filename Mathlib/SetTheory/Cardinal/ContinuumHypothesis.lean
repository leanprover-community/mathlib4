/-
Copyright (c) 2026 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.SetTheory.Cardinal.Continuum
import Mathlib.Analysis.Real.Cardinality

/-!
# The `ContinuumHypothesis` typeclass

We make this a typeclass rather than an axiom so that it is immediately obvious when a theorem
assumes this hypothesis.

In mathlib, we show consequences of the continuum hpyothesis with `[ContinuumHypothesis]` in
assumptions.
If in downstream projects you want to assume this as an axiom, you can write
```
@[instance] axiom continuumHypothesis : ContinuumHypothesis
```

## Main results

* `ContinuumHypothesis.iff_exists_sierpinski_pathological_partition`: SierpiÅ„ski's 1919 theorem.
-/

open scoped Cardinal

/-- The statement that the continuum hypothesis holds.

To avoid a universe parameter, we only state that this holds in universe `0`, since it can be lifted
to other universes with subsequent theorems.

See `ContinuumHypothesis.iff_aleph0_covby_continuum` and
`ContinuumHypothesis.iff_continuum_eq_aleph_one` for typical characterizations.
-/
class ContinuumHypothesis where
  /-- See `ContinuumHypothesis.of_continuum_eq_aleph_one'` for the universe-generic version. -/
  of_continuum_eq_aleph_one' ::
    /-- See `ContinuumHypothesis.continuum_eq_aleph_one` for the universe-generic version. -/
    continuum_eq_aleph_one' : (ğ”  : Cardinal.{0}) = â„µâ‚

namespace ContinuumHypothesis

/-- See `ContinuumHypothesis.iff_continuum_eq_aleph_one` for the universe-generic version. -/
theorem iff_continuum_eq_aleph_one' : ContinuumHypothesis â†” (ğ”  : Cardinal.{0}) = â„µâ‚ :=
  âŸ¨(Â·.continuum_eq_aleph_one'), .of_continuum_eq_aleph_one'âŸ©

section basic_constructors

theorem iff_continuum_eq_aleph_one.{u} : ContinuumHypothesis â†” (ğ”  : Cardinal.{u}) = â„µâ‚ := by
  rw [iff_continuum_eq_aleph_one', â† Cardinal.lift_continuum.{u, 0}, Cardinal.lift_eq_aleph_one]

theorem continuum_eq_aleph_one.{u} [ContinuumHypothesis] : (ğ”  : Cardinal.{u}) = â„µâ‚ :=
  iff_continuum_eq_aleph_one.1 â€¹_â€º

alias âŸ¨_, of_continuum_eq_aleph_oneâŸ© := iff_continuum_eq_aleph_one

theorem iff_aleph0_covby_continuum : ContinuumHypothesis â†” â„µâ‚€ â‹– ğ”  := by
  rw [â† Order.succ_eq_iff_covBy, Cardinal.succ_aleph0, eq_comm, iff_continuum_eq_aleph_one]

theorem aleph0_covby_continuum.{u} [ContinuumHypothesis] : â„µâ‚€ â‹– (ğ”  : Cardinal.{u}) :=
  iff_aleph0_covby_continuum.1 â€¹_â€º

alias âŸ¨_, of_aleph0_covby_continuumâŸ© := iff_aleph0_covby_continuum

end basic_constructors

open Set in
theorem exists_sierpinski_pathological_pred [ContinuumHypothesis] :
    âˆƒ f : â„ â†’ â„ â†’ Prop, (âˆ€ x, {y | Â¬ f x y}.Countable) âˆ§ âˆ€ y, {x : â„ | f x y}.Countable := by
  have Hcont : #â„ = â„µâ‚ := by rw [â† continuum_eq_aleph_one, Cardinal.mk_real]
  rcases Cardinal.ord_eq â„ with âŸ¨r, hr, HâŸ©
  refine âŸ¨r, fun x => ?_, fun y => ?_âŸ©
  Â· have : {y | Â¬r x y} = {y | r y x} âˆª {x} := by
      ext y
      simp only [mem_setOf_eq, mem_insert_iff, union_singleton]
      rcases trichotomous_of r x y with (h | rfl | h)
      Â· simp only [h, not_or, false_iff, not_true]
        constructor
        Â· rintro rfl; exact irrefl_of r y h
        Â· exact asymm h
      Â· simp only [true_or, iff_true]; exact irrefl x
      Â· simp only [h, iff_true, or_true]; exact asymm h
    rw [this]
    apply Countable.union _ (countable_singleton _)
    rw [Cardinal.countable_iff_lt_aleph_one, â† Hcont]
    exact Cardinal.card_typein_lt r x H
  Â· rw [Cardinal.countable_iff_lt_aleph_one, â† Hcont]
    exact Cardinal.card_typein_lt r y H

/--
Alternate statement of `iff_exists_sierpinski_pathological_partition` using a predicate to assign
points to sets.
-/
theorem iff_exists_sierpinski_pathological_pred :
    ContinuumHypothesis â†”
      âˆƒ f : â„ â†’ â„ â†’ Prop, (âˆ€ x, {y | Â¬ f x y}.Countable) âˆ§ âˆ€ y, {x : â„ | f x y}.Countable := by
  refine âŸ¨(Â·.exists_sierpinski_pathological_pred), fun âŸ¨f, hnf, hfâŸ© => ?_âŸ©
  suffices #â„ â‰¤ â„µâ‚ by
    rw [iff_continuum_eq_aleph_one]
    apply le_antisymm (Cardinal.mk_real.symm.trans_le this)
    exact Cardinal.aleph_one_le_continuum
  by_contra! h_c_gt
  obtain âŸ¨S, hS_card : #S = â„µâ‚âŸ© := Cardinal.le_mk_iff_exists_set.mp h_c_gt.le
  haveI : Nonempty S := by
    rw [Set.nonempty_coe_sort, â† Cardinal.mk_set_ne_zero_iff, hS_card]
    exact (Cardinal.aleph_pos _).ne'
  let C := â‹ƒ x âˆˆ S, {y | Â¬ f x y}
  have hC_le : #C â‰¤ â„µâ‚ := by
    calc #C â‰¤ #S * â„µâ‚€ := by
            grw [Cardinal.mk_biUnion_le _ _]
            gcongr
            refine ciSup_le fun x => ?_
            grw [Cardinal.le_aleph0_iff_set_countable.2 (hnf _)]
         _ = â„µâ‚ * â„µâ‚€ := by rw [hS_card]
         _ = â„µâ‚      := Cardinal.aleph_mul_aleph0 1
  obtain âŸ¨y', hy_notin : y' âˆ‰ CâŸ© := Cardinal.compl_nonempty_of_mk_lt_mk (hC_le.trans_lt h_c_gt)
  have h_sub : S âŠ† {x | f x y'} := by simpa [C] using hy_notin
  have h_S_count : S.Countable := (hf y').mono h_sub
  rw [â† Cardinal.le_aleph0_iff_set_countable, hS_card] at h_S_count
  exact h_S_count.not_gt Cardinal.aleph0_lt_aleph_one

/-- Forward direction of `iff_exists_sierpinski_pathological_partition`. -/
theorem exists_sierpinski_pathological_partition [ContinuumHypothesis] :
    âˆƒ S T : Set (â„ Ã— â„), IsCompl S T âˆ§
      (âˆ€ x, {y | (x, y) âˆˆ S}.Countable) âˆ§ âˆ€ y, {x : â„ | (x, y) âˆˆ T}.Countable := by
  let âŸ¨f, hS, hTâŸ© := exists_sierpinski_pathological_pred
  refine âŸ¨{p | Â¬f p.1 p.2}, {p | f p.1 p.2}, isCompl_compl.symm, hS, hTâŸ©

/--
The continuum hypothesis is equivalent to the statement that
the plane $\mathbb{R}^2$ can be partitioned into two sets,
$A$ and $B$, such that every horizontal line intersects $A$ in a countable number of points,
and every vertical line intersects $B$ in a countable number of points.

This is SierpiÅ„ski (1919).
-/
theorem iff_exists_sierpinski_pathological_partition :
    ContinuumHypothesis â†”
      âˆƒ S T : Set (â„ Ã— â„), IsCompl S T âˆ§
        (âˆ€ x, {y | (x, y) âˆˆ S}.Countable) âˆ§ âˆ€ y, {x : â„ | (x, y) âˆˆ T}.Countable := by
  refine âŸ¨(Â·.exists_sierpinski_pathological_partition), ?_âŸ©
  rw [iff_exists_sierpinski_pathological_pred]
  rintro âŸ¨S, T, hST, hS, hTâŸ©
  obtain rfl := hST.eq_compl
  refine âŸ¨fun x y => (x, y) âˆˆ T, hS, hTâŸ©

end ContinuumHypothesis
