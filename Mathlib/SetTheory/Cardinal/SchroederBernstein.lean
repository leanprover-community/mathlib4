/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro
-/
import Mathlib.Order.FixedPoints
import Mathlib.Order.Zorn

#align_import set_theory.cardinal.schroeder_bernstein from "leanprover-community/mathlib"@"1e05171a5e8cf18d98d9cf7b207540acb044acae"

/-!
# Schr√∂der-Bernstein theorem, well-ordering of cardinals

This file proves the Schr√∂der-Bernstein theorem (see `schroeder_bernstein`), the well-ordering of
cardinals (see `min_injective`) and the totality of their order (see `total`).

## Notes

Cardinals are naturally ordered by `Œ± ‚â§ Œ≤ ‚Üî ‚àÉ f : a ‚Üí Œ≤, Injective f`:
* `schroeder_bernstein` states that, given injections `Œ± ‚Üí Œ≤` and `Œ≤ ‚Üí Œ±`, one can get a
  bijection `Œ± ‚Üí Œ≤`. This corresponds to the antisymmetry of the order.
* The order is also well-founded: any nonempty set of cardinals has a minimal element.
  `min_injective` states that by saying that there exists an element of the set that injects into
  all others.

Cardinals are defined and further developed in the folder `SetTheory.Cardinal`.
-/


open Set Function

open Classical

universe u v

namespace Function

namespace Embedding

section antisymm

variable {Œ± : Type u} {Œ≤ : Type v}

/-- **The Schr√∂der-Bernstein Theorem**:
Given injections `Œ± ‚Üí Œ≤` and `Œ≤ ‚Üí Œ±`, we can get a bijection `Œ± ‚Üí Œ≤`. -/
theorem schroeder_bernstein {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ±} (hf : Function.Injective f)
    (hg : Function.Injective g) : ‚àÉ h : Œ± ‚Üí Œ≤, Bijective h := by
  cases' isEmpty_or_nonempty Œ≤ with hŒ≤ hŒ≤
  -- ‚ä¢ ‚àÉ h, Bijective h
  ¬∑ have : IsEmpty Œ± := Function.isEmpty f
    -- ‚ä¢ ‚àÉ h, Bijective h
    exact ‚ü®_, ((Equiv.equivEmpty Œ±).trans (Equiv.equivEmpty Œ≤).symm).bijective‚ü©
    -- üéâ no goals
  set F : Set Œ± ‚Üío Set Œ± :=
    { toFun := fun s => (g '' (f '' s)·∂ú)·∂ú
      monotone' := fun s t hst =>
        compl_subset_compl.mpr <| image_subset _ <| compl_subset_compl.mpr <| image_subset _ hst }
  -- Porting note: dot notation `F.lfp` doesn't work here
  set s : Set Œ± := OrderHom.lfp F
  -- ‚ä¢ ‚àÉ h, Bijective h
  have hs : (g '' (f '' s)·∂ú)·∂ú = s := F.map_lfp
  -- ‚ä¢ ‚àÉ h, Bijective h
  have hns : g '' (f '' s)·∂ú = s·∂ú := compl_injective (by simp [hs])
  -- ‚ä¢ ‚àÉ h, Bijective h
  set g' := invFun g
  -- ‚ä¢ ‚àÉ h, Bijective h
  have g'g : LeftInverse g' g := leftInverse_invFun hg
  -- ‚ä¢ ‚àÉ h, Bijective h
  have hg'ns : g' '' s·∂ú = (f '' s)·∂ú := by rw [‚Üê hns, g'g.image_image]
  -- ‚ä¢ ‚àÉ h, Bijective h
  set h : Œ± ‚Üí Œ≤ := s.piecewise f g'
  -- ‚ä¢ ‚àÉ h, Bijective h
  have : Surjective h := by rw [‚Üê range_iff_surjective, range_piecewise, hg'ns, union_compl_self]
  -- ‚ä¢ ‚àÉ h, Bijective h
  have : Injective h := by
    refine' (injective_piecewise_iff _).2 ‚ü®hf.injOn _, _, _‚ü©
    ¬∑ intro x hx y hy hxy
      obtain ‚ü®x', _, rfl‚ü© : x ‚àà g '' (f '' s)·∂ú := by rwa [hns]
      obtain ‚ü®y', _, rfl‚ü© : y ‚àà g '' (f '' s)·∂ú := by rwa [hns]
      rw [g'g _, g'g _] at hxy
      rw [hxy]
    ¬∑ intro x hx y hy hxy
      obtain ‚ü®y', hy', rfl‚ü© : y ‚àà g '' (f '' s)·∂ú := by rwa [hns]
      rw [g'g _] at hxy
      exact hy' ‚ü®x, hx, hxy‚ü©
  exact ‚ü®h, ‚ÄπInjective h‚Ä∫, ‚ÄπSurjective h‚Ä∫‚ü©
  -- üéâ no goals
#align function.embedding.schroeder_bernstein Function.Embedding.schroeder_bernstein

/-- **The Schr√∂der-Bernstein Theorem**: Given embeddings `Œ± ‚Ü™ Œ≤` and `Œ≤ ‚Ü™ Œ±`, there exists an
equivalence `Œ± ‚âÉ Œ≤`. -/
theorem antisymm : (Œ± ‚Ü™ Œ≤) ‚Üí (Œ≤ ‚Ü™ Œ±) ‚Üí Nonempty (Œ± ‚âÉ Œ≤)
  | ‚ü®_, h‚ÇÅ‚ü©, ‚ü®_, h‚ÇÇ‚ü© =>
    let ‚ü®f, hf‚ü© := schroeder_bernstein h‚ÇÅ h‚ÇÇ
    ‚ü®Equiv.ofBijective f hf‚ü©
#align function.embedding.antisymm Function.Embedding.antisymm

end antisymm

section Wo

variable {Œπ : Type u} (Œ≤ : Œπ ‚Üí Type v)

/-- `sets Œ≤` -/
@[reducible]
private def sets :=
  { s : Set (‚àÄ i, Œ≤ i) | ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, ‚àÄ (i), (x : ‚àÄ i, Œ≤ i) i = y i ‚Üí x = y }

/-- The cardinals are well-ordered. We express it here by the fact that in any set of cardinals
there is an element that injects into the others.
See `Cardinal.conditionallyCompleteLinearOrderBot` for (one of) the lattice instances. -/
theorem min_injective [I : Nonempty Œπ] : ‚àÉ i, Nonempty (‚àÄ j, Œ≤ i ‚Ü™ Œ≤ j) :=
  let ‚ü®s, hs, ms‚ü© :=
    show ‚àÉ s ‚àà sets Œ≤, ‚àÄ a ‚àà sets Œ≤, s ‚äÜ a ‚Üí a = s from
      zorn_subset (sets Œ≤) fun c hc hcc =>
        ‚ü®‚ãÉ‚ÇÄc, fun x ‚ü®p, hpc, hxp‚ü© y ‚ü®q, hqc, hyq‚ü© i hi =>
          (hcc.total hpc hqc).elim (fun h => hc hqc x (h hxp) y hyq i hi) fun h =>
            hc hpc x hxp y (h hyq) i hi,
          fun _ => subset_sUnion_of_mem‚ü©
  let ‚ü®i, e‚ü© :=
    show ‚àÉ i, ‚àÄ y, ‚àÉ x ‚àà s, (x : ‚àÄ i, Œ≤ i) i = y from
      Classical.by_contradiction fun h =>
        have h : ‚àÄ i, ‚àÉ y, ‚àÄ x ‚àà s, (x : ‚àÄ i, Œ≤ i) i ‚â† y := by
          simpa only [ne_eq, not_exists, not_forall, not_and] using h
          -- üéâ no goals
        let ‚ü®f, hf‚ü© := Classical.axiom_of_choice h
        have : f ‚àà s :=
          have : insert f s ‚àà sets Œ≤ := fun x hx y hy => by
            cases' hx with hx hx <;> cases' hy with hy hy; ¬∑ simp [hx, hy]
            -- ‚ä¢ ‚àÄ (i : Œπ), x i = y i ‚Üí x = y
                                     -- ‚ä¢ ‚àÄ (i : Œπ), x i = y i ‚Üí x = y
                                     -- ‚ä¢ ‚àÄ (i : Œπ), x i = y i ‚Üí x = y
                                                             -- üéâ no goals
            ¬∑ subst x
              -- ‚ä¢ ‚àÄ (i : Œπ), f i = y i ‚Üí f = y
              exact fun i e => (hf i y hy e.symm).elim
              -- üéâ no goals
            ¬∑ subst y
              -- ‚ä¢ ‚àÄ (i : Œπ), x i = f i ‚Üí x = f
              exact fun i e => (hf i x hx e).elim
              -- üéâ no goals
            ¬∑ exact hs x hx y hy
              -- üéâ no goals
          ms _ this (subset_insert f s) ‚ñ∏ mem_insert _ _
        let ‚ü®i‚ü© := I
        hf i f this rfl
  let ‚ü®f, hf‚ü© := Classical.axiom_of_choice e
  ‚ü®i,
    ‚ü®fun j =>
      ‚ü®fun a => f a j, fun a b e' => by
        let ‚ü®sa, ea‚ü© := hf a
        -- ‚ä¢ a = b
        let ‚ü®sb, eb‚ü© := hf b
        -- ‚ä¢ a = b
        rw [‚Üê ea, ‚Üê eb, hs _ sa _ sb _ e']‚ü©‚ü©‚ü©
        -- üéâ no goals
#align function.embedding.min_injective Function.Embedding.min_injective

end Wo

/-- The cardinals are totally ordered. See
`Cardinal.conditionallyCompleteLinearOrderBot` for (one of) the lattice
instance. -/
-- Porting note: `ULift.{max u v, u} Œ±` was `ULift Œ±`
theorem total (Œ± : Type u) (Œ≤ : Type v) : Nonempty (Œ± ‚Ü™ Œ≤) ‚à® Nonempty (Œ≤ ‚Ü™ Œ±) :=
  match @min_injective Bool (fun b => cond b (ULift.{max u v, u} Œ±) (ULift.{max u v, v} Œ≤)) ‚ü®true‚ü©
    with
  | ‚ü®true, ‚ü®h‚ü©‚ü© =>
    let ‚ü®f, hf‚ü© := h false
    Or.inl ‚ü®Embedding.congr Equiv.ulift Equiv.ulift ‚ü®f, hf‚ü©‚ü©
  | ‚ü®false, ‚ü®h‚ü©‚ü© =>
    let ‚ü®f, hf‚ü© := h true
    Or.inr ‚ü®Embedding.congr Equiv.ulift Equiv.ulift ‚ü®f, hf‚ü©‚ü©
#align function.embedding.total Function.Embedding.total

end Embedding

end Function
