/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Floris van Doorn
-/
import Mathlib.Algebra.Order.GroupWithZero.Canonical
import Mathlib.Algebra.Order.Ring.Canonical
import Mathlib.Data.Fintype.Option
import Mathlib.Order.InitialSeg
import Mathlib.Order.Nat
import Mathlib.Order.SuccPred.CompleteLinearOrder
import Mathlib.SetTheory.Cardinal.Defs
import Mathlib.SetTheory.Cardinal.SchroederBernstein

/-!
# Order on cardinal numbers

We define the order on cardinal numbers and show its basic properties, including the ordered
semiring structure.

## Main definitions

* The order `c‚ÇÅ ‚â§ c‚ÇÇ` is defined by `Cardinal.le_def Œ± Œ≤ : #Œ± ‚â§ #Œ≤ ‚Üî Nonempty (Œ± ‚Ü™ Œ≤)`.
* `Order.IsSuccLimit c` means that `c` is a (weak) limit cardinal: `c ‚â† 0 ‚àß ‚àÄ x < c, succ x < c`.
* `Cardinal.IsStrongLimit c` means that `c` is a strong limit cardinal:
  `c ‚â† 0 ‚àß ‚àÄ x < c, 2 ^ x < c`.

## Main instances

* Cardinals form a `CanonicallyOrderedAdd` `OrderedCommSemiring` with the aforementioned sum and
  product.
* Cardinals form a `SuccOrder`. Use `Order.succ c` for the smallest cardinal greater than `c`.
* The less-than relation on cardinals forms a well-order.
* Cardinals form a `ConditionallyCompleteLinearOrderBot`. Bounded sets for cardinals in universe
  `u` are precisely the sets indexed by some type in universe `u`, see
  `Cardinal.bddAbove_iff_small`. One can use `sSup` for the cardinal supremum,
  and `sInf` for the minimum of a set of cardinals.

## Main statements

* Cantor's theorem: `Cardinal.cantor c : c < 2 ^ c`.
* K√∂nig's theorem: `Cardinal.sum_lt_prod`

## Implementation notes

The current setup interweaves the order structure and the algebraic structure on `Cardinal` tightly.
For example, we need to know what a ring is in order to show that `0` is the smallest cardinality.
That is reflected in this file containing both the order and algebra structure.

## References

* <https://en.wikipedia.org/wiki/Cardinal_number>

## Tags

cardinal number, cardinal arithmetic, cardinal exponentiation, aleph,
Cantor's theorem, K√∂nig's theorem, Konig's theorem
-/

assert_not_exists Field

open List Function Order Set

noncomputable section

universe u v w v' w'

variable {Œ± Œ≤ : Type u}

namespace Cardinal

/-! ### Order on cardinals -/

/-- We define the order on cardinal numbers by `#Œ± ‚â§ #Œ≤` if and only if
  there exists an embedding (injective function) from Œ± to Œ≤. -/
instance : LE Cardinal.{u} :=
  ‚ü®fun q‚ÇÅ q‚ÇÇ =>
    Quotient.liftOn‚ÇÇ q‚ÇÅ q‚ÇÇ (fun Œ± Œ≤ => Nonempty <| Œ± ‚Ü™ Œ≤) fun _ _ _ _ ‚ü®e‚ÇÅ‚ü© ‚ü®e‚ÇÇ‚ü© =>
      propext ‚ü®fun ‚ü®e‚ü© => ‚ü®e.congr e‚ÇÅ e‚ÇÇ‚ü©, fun ‚ü®e‚ü© => ‚ü®e.congr e‚ÇÅ.symm e‚ÇÇ.symm‚ü©‚ü©‚ü©

instance partialOrder : PartialOrder Cardinal.{u} where
  le := (¬∑ ‚â§ ¬∑)
  le_refl := by
    rintro ‚ü®Œ±‚ü©
    exact ‚ü®Embedding.refl _‚ü©
  le_trans := by
    rintro ‚ü®Œ±‚ü© ‚ü®Œ≤‚ü© ‚ü®Œ≥‚ü© ‚ü®e‚ÇÅ‚ü© ‚ü®e‚ÇÇ‚ü©
    exact ‚ü®e‚ÇÅ.trans e‚ÇÇ‚ü©
  le_antisymm := by
    rintro ‚ü®Œ±‚ü© ‚ü®Œ≤‚ü© ‚ü®e‚ÇÅ‚ü© ‚ü®e‚ÇÇ‚ü©
    exact Quotient.sound (e‚ÇÅ.antisymm e‚ÇÇ)

instance linearOrder : LinearOrder Cardinal.{u} :=
  { Cardinal.partialOrder with
    le_total := by
      rintro ‚ü®Œ±‚ü© ‚ü®Œ≤‚ü©
      apply Embedding.total
    toDecidableLE := Classical.decRel _ }

theorem le_def (Œ± Œ≤ : Type u) : #Œ± ‚â§ #Œ≤ ‚Üî Nonempty (Œ± ‚Ü™ Œ≤) :=
  Iff.rfl

theorem mk_le_of_injective {Œ± Œ≤ : Type u} {f : Œ± ‚Üí Œ≤} (hf : Injective f) : #Œ± ‚â§ #Œ≤ :=
  ‚ü®‚ü®f, hf‚ü©‚ü©

theorem _root_.Function.Embedding.cardinal_le {Œ± Œ≤ : Type u} (f : Œ± ‚Ü™ Œ≤) : #Œ± ‚â§ #Œ≤ :=
  ‚ü®f‚ü©

theorem mk_le_of_surjective {Œ± Œ≤ : Type u} {f : Œ± ‚Üí Œ≤} (hf : Surjective f) : #Œ≤ ‚â§ #Œ± :=
  ‚ü®Embedding.ofSurjective f hf‚ü©

theorem le_mk_iff_exists_set {c : Cardinal} {Œ± : Type u} : c ‚â§ #Œ± ‚Üî ‚àÉ p : Set Œ±, #p = c :=
  ‚ü®inductionOn c fun _ ‚ü®‚ü®f, hf‚ü©‚ü© => ‚ü®Set.range f, (Equiv.ofInjective f hf).cardinal_eq.symm‚ü©,
    fun ‚ü®_, e‚ü© => e ‚ñ∏ ‚ü®‚ü®Subtype.val, fun _ _ => Subtype.eq‚ü©‚ü©‚ü©

theorem mk_subtype_le {Œ± : Type u} (p : Œ± ‚Üí Prop) : #(Subtype p) ‚â§ #Œ± :=
  ‚ü®Embedding.subtype p‚ü©

theorem mk_set_le (s : Set Œ±) : #s ‚â§ #Œ± :=
  mk_subtype_le s

theorem out_embedding {c c' : Cardinal} : c ‚â§ c' ‚Üî Nonempty (c.out ‚Ü™ c'.out) := by
  conv_lhs => rw [‚Üê Cardinal.mk_out c, ‚Üê Cardinal.mk_out c', le_def]

theorem lift_mk_le {Œ± : Type v} {Œ≤ : Type w} :
    lift.{max u w} #Œ± ‚â§ lift.{max u v} #Œ≤ ‚Üî Nonempty (Œ± ‚Ü™ Œ≤) :=
  ‚ü®fun ‚ü®f‚ü© => ‚ü®Embedding.congr Equiv.ulift Equiv.ulift f‚ü©, fun ‚ü®f‚ü© =>
    ‚ü®Embedding.congr Equiv.ulift.symm Equiv.ulift.symm f‚ü©‚ü©

/-- A variant of `Cardinal.lift_mk_le` with specialized universes.
Because Lean often cannot realize it should use this specialization itself,
we provide this statement separately so you don't have to solve the specialization problem either.
-/
theorem lift_mk_le' {Œ± : Type u} {Œ≤ : Type v} : lift.{v} #Œ± ‚â§ lift.{u} #Œ≤ ‚Üî Nonempty (Œ± ‚Ü™ Œ≤) :=
  lift_mk_le.{0}

/-! ### `lift` sends `Cardinal.{u}` to an initial segment of `Cardinal.{max u v}`. -/

/-- `Cardinal.lift` as an `InitialSeg`. -/
@[simps!]
def liftInitialSeg : Cardinal.{u} ‚â§i Cardinal.{max u v} := by
  refine ‚ü®(OrderEmbedding.ofMapLEIff lift ?_).ltEmbedding, ?_‚ü© <;> intro a b
  ¬∑ refine inductionOn‚ÇÇ a b fun _ _ ‚Ü¶ ?_
    rw [‚Üê lift_umax, lift_mk_le.{v, u, u}, le_def]
  ¬∑ refine inductionOn‚ÇÇ a b fun Œ± Œ≤ h ‚Ü¶ ?_
    obtain ‚ü®e‚ü© := h.le
    replace e := e.congr (Equiv.refl Œ≤) Equiv.ulift
    refine ‚ü®#(range e), mk_congr (Equiv.ulift.trans <| Equiv.symm ?_)‚ü©
    apply (e.codRestrict _ mem_range_self).equivOfSurjective
    rintro ‚ü®a, ‚ü®b, rfl‚ü©‚ü©
    exact ‚ü®b, rfl‚ü©

theorem mem_range_lift_of_le {a : Cardinal.{u}} {b : Cardinal.{max u v}} :
    b ‚â§ lift.{v, u} a ‚Üí b ‚àà Set.range lift.{v, u} :=
  liftInitialSeg.mem_range_of_le

theorem lift_injective : Injective lift.{u, v} :=
  liftInitialSeg.injective

@[simp]
theorem lift_inj {a b : Cardinal.{u}} : lift.{v, u} a = lift.{v, u} b ‚Üî a = b :=
  lift_injective.eq_iff

@[simp]
theorem lift_le {a b : Cardinal.{v}} : lift.{u} a ‚â§ lift.{u} b ‚Üî a ‚â§ b :=
  liftInitialSeg.le_iff_le

@[simp]
theorem lift_lt {a b : Cardinal.{u}} : lift.{v, u} a < lift.{v, u} b ‚Üî a < b :=
  liftInitialSeg.lt_iff_lt

theorem lift_strictMono : StrictMono lift := fun _ _ => lift_lt.2

theorem lift_monotone : Monotone lift :=
  lift_strictMono.monotone

@[simp]
theorem lift_min {a b : Cardinal} : lift.{u, v} (min a b) = min (lift.{u, v} a) (lift.{u, v} b) :=
  lift_monotone.map_min

@[simp]
theorem lift_max {a b : Cardinal} : lift.{u, v} (max a b) = max (lift.{u, v} a) (lift.{u, v} b) :=
  lift_monotone.map_max

-- This cannot be a `@[simp]` lemma because `simp` can't figure out the universes.
theorem lift_umax_eq {a : Cardinal.{u}} {b : Cardinal.{v}} :
    lift.{max v w} a = lift.{max u w} b ‚Üî lift.{v} a = lift.{u} b := by
  rw [‚Üê lift_lift.{v, w, u}, ‚Üê lift_lift.{u, w, v}, lift_inj]

theorem le_lift_iff {a : Cardinal.{u}} {b : Cardinal.{max u v}} :
    b ‚â§ lift.{v, u} a ‚Üî ‚àÉ a' ‚â§ a, lift.{v, u} a' = b :=
  liftInitialSeg.le_apply_iff

theorem lt_lift_iff {a : Cardinal.{u}} {b : Cardinal.{max u v}} :
    b < lift.{v, u} a ‚Üî ‚àÉ a' < a, lift.{v, u} a' = b :=
  liftInitialSeg.lt_apply_iff

/-! ### Basic cardinals -/

@[simp]
theorem lift_eq_zero {a : Cardinal.{v}} : lift.{u} a = 0 ‚Üî a = 0 :=
  lift_injective.eq_iff' lift_zero

@[simp]
theorem mk_fintype (Œ± : Type u) [h : Fintype Œ±] : #Œ± = Fintype.card Œ± :=
  mk_congr (Fintype.equivOfCardEq (by simp))

private theorem cast_succ (n : ‚Ñï) : ((n + 1 : ‚Ñï) : Cardinal.{u}) = n + 1 := by
  change #(ULift.{u} _) = #(ULift.{u} _) + 1
  rw [‚Üê mk_option]
  simp

instance commSemiring : CommSemiring Cardinal.{u} where
  zero := 0
  one := 1
  add := (¬∑ + ¬∑)
  mul := (¬∑ * ¬∑)
  zero_add a := inductionOn a fun Œ± => mk_congr <| Equiv.emptySum _ Œ±
  add_zero a := inductionOn a fun Œ± => mk_congr <| Equiv.sumEmpty Œ± _
  add_assoc a b c := inductionOn‚ÇÉ a b c fun Œ± Œ≤ Œ≥ => mk_congr <| Equiv.sumAssoc Œ± Œ≤ Œ≥
  add_comm a b := inductionOn‚ÇÇ a b fun Œ± Œ≤ => mk_congr <| Equiv.sumComm Œ± Œ≤
  zero_mul a := inductionOn a fun _ => mk_eq_zero _
  mul_zero a := inductionOn a fun _ => mk_eq_zero _
  one_mul a := inductionOn a fun Œ± => mk_congr <| Equiv.uniqueProd Œ± _
  mul_one a := inductionOn a fun Œ± => mk_congr <| Equiv.prodUnique Œ± _
  mul_assoc a b c := inductionOn‚ÇÉ a b c fun Œ± Œ≤ Œ≥ => mk_congr <| Equiv.prodAssoc Œ± Œ≤ Œ≥
  mul_comm a b := inductionOn‚ÇÇ a b fun Œ± Œ≤ => mk_congr <| Equiv.prodComm Œ± Œ≤
  left_distrib a b c := inductionOn‚ÇÉ a b c fun Œ± Œ≤ Œ≥ => mk_congr <| Equiv.prodSumDistrib Œ± Œ≤ Œ≥
  right_distrib a b c := inductionOn‚ÇÉ a b c fun Œ± Œ≤ Œ≥ => mk_congr <| Equiv.sumProdDistrib Œ± Œ≤ Œ≥
  nsmul := nsmulRec
  npow n c := c ^ (n : Cardinal)
  npow_zero := power_zero
  npow_succ n c := by rw [cast_succ, power_add, power_one]
  natCast n := lift #(Fin n)
  natCast_zero := rfl
  natCast_succ n := cast_succ n

theorem mk_bool : #Bool = 2 := by simp

theorem mk_Prop : #Prop = 2 := by simp

theorem power_mul {a b c : Cardinal} : a ^ (b * c) = (a ^ b) ^ c := by
  rw [mul_comm b c]
  exact inductionOn‚ÇÉ a b c fun Œ± Œ≤ Œ≥ => mk_congr <| Equiv.curry Œ≥ Œ≤ Œ±

@[simp, norm_cast]
theorem power_natCast (a : Cardinal.{u}) (n : ‚Ñï) : a ^ (‚Üën : Cardinal.{u}) = a ^ n :=
  rfl

@[simp]
theorem lift_eq_one {a : Cardinal.{v}} : lift.{u} a = 1 ‚Üî a = 1 :=
  lift_injective.eq_iff' lift_one

@[simp]
theorem lift_mul (a b : Cardinal.{u}) : lift.{v} (a * b) = lift.{v} a * lift.{v} b :=
  inductionOn‚ÇÇ a b fun _ _ =>
    mk_congr <| Equiv.ulift.trans (Equiv.prodCongr Equiv.ulift Equiv.ulift).symm

theorem lift_two : lift.{u, v} 2 = 2 := by simp [‚Üê one_add_one_eq_two]

@[simp]
theorem mk_set {Œ± : Type u} : #(Set Œ±) = 2 ^ #Œ± := by simp [‚Üê one_add_one_eq_two, Set]

/-- A variant of `Cardinal.mk_set` expressed in terms of a `Set` instead of a `Type`. -/
@[simp]
theorem mk_powerset {Œ± : Type u} (s : Set Œ±) : #(‚Ü•(ùí´ s)) = 2 ^ #(‚Ü•s) :=
  (mk_congr (Equiv.Set.powerset s)).trans mk_set

theorem lift_two_power (a : Cardinal) : lift.{v} (2 ^ a) = 2 ^ lift.{v} a := by
  simp [‚Üê one_add_one_eq_two]

/-! ### Order properties -/

protected theorem zero_le : ‚àÄ a : Cardinal, 0 ‚â§ a := by
  rintro ‚ü®Œ±‚ü©
  exact ‚ü®Embedding.ofIsEmpty‚ü©

private theorem add_le_add' : ‚àÄ {a b c d : Cardinal}, a ‚â§ b ‚Üí c ‚â§ d ‚Üí a + c ‚â§ b + d := by
  rintro ‚ü®Œ±‚ü© ‚ü®Œ≤‚ü© ‚ü®Œ≥‚ü© ‚ü®Œ¥‚ü© ‚ü®e‚ÇÅ‚ü© ‚ü®e‚ÇÇ‚ü©; exact ‚ü®e‚ÇÅ.sumMap e‚ÇÇ‚ü©

instance addLeftMono : AddLeftMono Cardinal :=
  ‚ü®fun _ _ _ => add_le_add' le_rfl‚ü©

instance addRightMono : AddRightMono Cardinal :=
  ‚ü®fun _ _ _ h => add_le_add' h le_rfl‚ü©

instance canonicallyOrderedAdd : CanonicallyOrderedAdd Cardinal.{u} where
  exists_add_of_le {a b} :=
    inductionOn‚ÇÇ a b fun Œ± Œ≤ ‚ü®‚ü®f, hf‚ü©‚ü© =>
      have : Œ± ‚äï ((range f)·∂ú : Set Œ≤) ‚âÉ Œ≤ := by
        classical
        exact (Equiv.sumCongr (Equiv.ofInjective f hf) (Equiv.refl _)).trans <|
          Equiv.Set.sumCompl (range f)
      ‚ü®#(‚Ü•(range f)·∂ú), mk_congr this.symm‚ü©
  le_self_add a b := (add_zero a).ge.trans <| by grw [Cardinal.zero_le b]
  le_add_self a _ := (zero_add a).ge.trans <| by grw [Cardinal.zero_le]

instance isOrderedRing : IsOrderedRing Cardinal.{u} :=
  CanonicallyOrderedAdd.toIsOrderedRing

instance orderBot : OrderBot Cardinal.{u} where
  bot := 0
  bot_le := zero_le

instance noZeroDivisors : NoZeroDivisors Cardinal.{u} where
  eq_zero_or_eq_zero_of_mul_eq_zero := fun {a b} =>
    inductionOn‚ÇÇ a b fun Œ± Œ≤ => by
      simpa only [mul_def, mk_eq_zero_iff, isEmpty_prod] using id

instance : LinearOrderedCommMonoidWithZero Cardinal.{u} :=
  { Cardinal.commSemiring,
    Cardinal.linearOrder with
    bot_le _ := bot_le
    mul_le_mul_left := @mul_le_mul_left' _ _ _ _
    zero_le_one := zero_le _ }

-- Computable instance to prevent a non-computable one being found via the one above
instance : CommMonoidWithZero Cardinal.{u} :=
  { Cardinal.commSemiring with }

-- Computable instance to prevent a non-computable one being found via the one above
instance : CommMonoid Cardinal.{u} :=
  { Cardinal.commSemiring with }

theorem zero_power_le (c : Cardinal.{u}) : (0 : Cardinal.{u}) ^ c ‚â§ 1 := by
  by_cases h : c = 0
  ¬∑ rw [h, power_zero]
  ¬∑ rw [zero_power h]
    apply zero_le

theorem power_le_power_left : ‚àÄ {a b c : Cardinal}, a ‚â† 0 ‚Üí b ‚â§ c ‚Üí a ^ b ‚â§ a ^ c := by
  rintro ‚ü®Œ±‚ü© ‚ü®Œ≤‚ü© ‚ü®Œ≥‚ü© hŒ± ‚ü®e‚ü©
  let ‚ü®a‚ü© := mk_ne_zero_iff.1 hŒ±
  exact ‚ü®@Function.Embedding.arrowCongrLeft _ _ _ ‚ü®a‚ü© e‚ü©

theorem self_le_power (a : Cardinal) {b : Cardinal} (hb : 1 ‚â§ b) : a ‚â§ a ^ b := by
  rcases eq_or_ne a 0 with (rfl | ha)
  ¬∑ exact zero_le _
  ¬∑ convert power_le_power_left ha hb
    exact (power_one a).symm

/-- **Cantor's theorem** -/
theorem cantor (a : Cardinal.{u}) : a < 2 ^ a := by
  induction a using Cardinal.inductionOn with | _ Œ±
  rw [‚Üê mk_set]
  refine ‚ü®‚ü®‚ü®singleton, fun a b => singleton_eq_singleton_iff.1‚ü©‚ü©, ?_‚ü©
  rintro ‚ü®‚ü®f, hf‚ü©‚ü©
  exact cantor_injective f hf

instance : NoMaxOrder Cardinal.{u} where exists_gt a := ‚ü®_, cantor a‚ü©

-- short-circuit type class inference
instance : DistribLattice Cardinal.{u} := inferInstance

theorem power_le_max_power_one {a b c : Cardinal} (h : b ‚â§ c) : a ^ b ‚â§ max (a ^ c) 1 := by
  by_cases ha : a = 0
  ¬∑ simp [ha, zero_power_le]
  ¬∑ exact (power_le_power_left ha h).trans (le_max_left _ _)

theorem power_le_power_right {a b c : Cardinal} : a ‚â§ b ‚Üí a ^ c ‚â§ b ^ c :=
  inductionOn‚ÇÉ a b c fun _ _ _ ‚ü®e‚ü© => ‚ü®Embedding.arrowCongrRight e‚ü©

theorem power_pos {a : Cardinal} (b : Cardinal) (ha : 0 < a) : 0 < a ^ b :=
  (power_ne_zero _ ha.ne').bot_lt

protected theorem lt_wf : @WellFounded Cardinal.{u} (¬∑ < ¬∑) :=
  ‚ü®fun a =>
    by_contradiction fun h => by
      let Œπ := { c : Cardinal // ¬¨Acc (¬∑ < ¬∑) c }
      let f : Œπ ‚Üí Cardinal := Subtype.val
      haveI hŒπ : Nonempty Œπ := ‚ü®‚ü®_, h‚ü©‚ü©
      obtain ‚ü®‚ü®c : Cardinal, hc : ¬¨Acc (¬∑ < ¬∑) c‚ü©, ‚ü®h_1 : ‚àÄ j, (f ‚ü®c, hc‚ü©).out ‚Ü™ (f j).out‚ü©‚ü© :=
        Embedding.min_injective fun i => (f i).out
      refine hc (Acc.intro _ fun j h' => by_contradiction fun hj => h'.2 ?_)
      have : #_ ‚â§ #_ := ‚ü®h_1 ‚ü®j, hj‚ü©‚ü©
      simpa only [mk_out] using this‚ü©

instance : WellFoundedRelation Cardinal.{u} :=
  ‚ü®(¬∑ < ¬∑), Cardinal.lt_wf‚ü©

instance : WellFoundedLT Cardinal.{u} :=
  ‚ü®Cardinal.lt_wf‚ü©

instance : ConditionallyCompleteLinearOrderBot Cardinal :=
  WellFoundedLT.conditionallyCompleteLinearOrderBot _

@[simp]
theorem sInf_empty : sInf (‚àÖ : Set Cardinal.{u}) = 0 :=
  dif_neg Set.not_nonempty_empty

/-- Note that the successor of `c` is not the same as `c + 1` except in the case of finite `c`. -/
instance : SuccOrder Cardinal := ConditionallyCompleteLinearOrder.toSuccOrder

theorem succ_def (c : Cardinal) : succ c = sInf { c' | c < c' } :=
  dif_neg <| not_isMax c

theorem succ_pos : ‚àÄ c : Cardinal, 0 < succ c :=
  bot_lt_succ

theorem succ_ne_zero (c : Cardinal) : succ c ‚â† 0 :=
  (succ_pos _).ne'

theorem add_one_le_succ (c : Cardinal.{u}) : c + 1 ‚â§ succ c := by
  have : Set.Nonempty { c' | c < c' } := exists_gt c
  simp_rw [succ_def, le_csInf_iff'' this, mem_setOf]
  intro b hlt
  rcases b, c with ‚ü®‚ü®Œ≤‚ü©, ‚ü®Œ≥‚ü©‚ü©
  obtain ‚ü®f‚ü© := le_of_lt hlt
  have : ¬¨Surjective f := fun hn => (not_le_of_gt hlt) (mk_le_of_surjective hn)
  simp only [Surjective, not_forall] at this
  rcases this with ‚ü®b, hb‚ü©
  calc
    #Œ≥ + 1 = #(Option Œ≥) := mk_option.symm
    _ ‚â§ #Œ≤ := (f.optionElim b hb).cardinal_le

@[simp]
theorem lift_succ (a) : lift.{v, u} (succ a) = succ (lift.{v, u} a) :=
  le_antisymm
    (le_of_not_gt fun h => by
      rcases lt_lift_iff.1 h with ‚ü®b, h, e‚ü©
      rw [lt_succ_iff, ‚Üê lift_le, e] at h
      exact h.not_gt (lt_succ _))
    (succ_le_of_lt <| lift_lt.2 <| lt_succ a)

/-! ### Limit cardinals -/

theorem ne_zero_of_isSuccLimit {c} (h : IsSuccLimit c) : c ‚â† 0 :=
  h.ne_bot

theorem isSuccPrelimit_zero : IsSuccPrelimit (0 : Cardinal) :=
  isSuccPrelimit_bot

protected theorem isSuccLimit_iff {c : Cardinal} : IsSuccLimit c ‚Üî c ‚â† 0 ‚àß IsSuccPrelimit c :=
  isSuccLimit_iff

@[simp]
protected theorem not_isSuccLimit_zero : ¬¨ IsSuccLimit (0 : Cardinal) :=
  not_isSuccLimit_bot

/-- A cardinal is a strong limit if it is not zero and it is closed under powersets.
Note that `‚Ñµ‚ÇÄ` is a strong limit by this definition. -/
structure IsStrongLimit (c : Cardinal) : Prop where
  ne_zero : c ‚â† 0
  two_power_lt ‚¶Éx‚¶Ñ : x < c ‚Üí 2 ^ x < c

protected theorem IsStrongLimit.isSuccLimit {c} (H : IsStrongLimit c) : IsSuccLimit c := by
  rw [Cardinal.isSuccLimit_iff]
  exact ‚ü®H.ne_zero, isSuccPrelimit_of_succ_lt fun x h ‚Ü¶
    (succ_le_of_lt <| cantor x).trans_lt (H.two_power_lt h)‚ü©

protected theorem IsStrongLimit.isSuccPrelimit {c} (H : IsStrongLimit c) : IsSuccPrelimit c :=
  H.isSuccLimit.isSuccPrelimit

@[simp]
theorem not_isStrongLimit_zero : ¬¨ IsStrongLimit (0 : Cardinal) :=
  fun h ‚Ü¶ h.ne_zero rfl

/-! ### Indexed cardinal `sum` -/

theorem le_sum {Œπ} (f : Œπ ‚Üí Cardinal) (i) : f i ‚â§ sum f := by
  rw [‚Üê Quotient.out_eq (f i)]
  exact ‚ü®‚ü®fun a => ‚ü®i, a‚ü©, fun a b h => by injection h‚ü©‚ü©

theorem iSup_le_sum {Œπ} (f : Œπ ‚Üí Cardinal) : iSup f ‚â§ sum f :=
  ciSup_le' <| le_sum _

@[simp]
theorem sum_add_distrib {Œπ} (f g : Œπ ‚Üí Cardinal) : sum (f + g) = sum f + sum g := by
  have := mk_congr (Equiv.sigmaSumDistrib (Quotient.out ‚àò f) (Quotient.out ‚àò g))
  simp only [comp_apply, mk_sigma, mk_sum, mk_out, lift_id] at this
  exact this

@[simp]
theorem sum_add_distrib' {Œπ} (f g : Œπ ‚Üí Cardinal) :
    (Cardinal.sum fun i => f i + g i) = sum f + sum g :=
  sum_add_distrib f g

@[gcongr]
theorem sum_le_sum {Œπ} (f g : Œπ ‚Üí Cardinal) (H : ‚àÄ i, f i ‚â§ g i) : sum f ‚â§ sum g :=
  ‚ü®(Embedding.refl _).sigmaMap fun i =>
      Classical.choice <| by have := H i; rwa [‚Üê Quot.out_eq (f i), ‚Üê Quot.out_eq (g i)] at this‚ü©

theorem mk_le_mk_mul_of_mk_preimage_le {c : Cardinal} (f : Œ± ‚Üí Œ≤) (hf : ‚àÄ b : Œ≤, #(f ‚Åª¬π' {b}) ‚â§ c) :
    #Œ± ‚â§ #Œ≤ * c := by
  simpa only [‚Üê mk_congr (@Equiv.sigmaFiberEquiv Œ± Œ≤ f), mk_sigma, ‚Üê sum_const'] using
    sum_le_sum _ _ hf

theorem lift_mk_le_lift_mk_mul_of_lift_mk_preimage_le {Œ± : Type u} {Œ≤ : Type v} {c : Cardinal}
    (f : Œ± ‚Üí Œ≤) (hf : ‚àÄ b : Œ≤, lift.{v} #(f ‚Åª¬π' {b}) ‚â§ c) : lift.{v} #Œ± ‚â§ lift.{u} #Œ≤ * c :=
  (mk_le_mk_mul_of_mk_preimage_le fun x : ULift.{v} Œ± => ULift.up.{u} (f x.1)) <|
    ULift.forall.2 fun b =>
      (mk_congr <|
            (Equiv.ulift.image _).trans
              (Equiv.trans
                (by
                  rw [Equiv.image_eq_preimage]
                  simp only [preimage, mem_singleton_iff, ULift.up_inj, mem_setOf_eq, coe_setOf]
                  exact Equiv.refl _)
                Equiv.ulift.symm)).trans_le
        (hf b)

end Cardinal

/-! ### Well-ordering theorem -/

open Cardinal in
theorem nonempty_embedding_to_cardinal : Nonempty (Œ± ‚Ü™ Cardinal.{u}) :=
  (Embedding.total _ _).resolve_left fun ‚ü®‚ü®f, hf‚ü©‚ü© =>
    let g : Œ± ‚Üí Cardinal.{u} := invFun f
    let ‚ü®x, (hx : g x = 2 ^ sum g)‚ü© := invFun_surjective hf (2 ^ sum g)
    have : g x ‚â§ sum g := le_sum.{u, u} g x
    not_le_of_gt (by rw [hx]; exact cantor _) this

/-- An embedding of any type to the set of cardinals in its universe. -/
def embeddingToCardinal : Œ± ‚Ü™ Cardinal.{u} :=
  Classical.choice nonempty_embedding_to_cardinal

/-- Any type can be endowed with a well order, obtained by pulling back the well order over
cardinals by some embedding. -/
def WellOrderingRel : Œ± ‚Üí Œ± ‚Üí Prop :=
  embeddingToCardinal ‚Åª¬π'o (¬∑ < ¬∑)

instance WellOrderingRel.isWellOrder : IsWellOrder Œ± WellOrderingRel :=
  (RelEmbedding.preimage _ _).isWellOrder

instance IsWellOrder.subtype_nonempty : Nonempty { r // IsWellOrder Œ± r } :=
  ‚ü®‚ü®WellOrderingRel, inferInstance‚ü©‚ü©

variable (Œ±) in
/-- The **well-ordering theorem** (or **Zermelo's theorem**): every type has a well-order -/
theorem exists_wellOrder : ‚àÉ (_ : LinearOrder Œ±), WellFoundedLT Œ± := by
  classical
  exact ‚ü®linearOrderOfSTO WellOrderingRel, WellOrderingRel.isWellOrder.toIsWellFounded‚ü©

namespace Cardinal

/-! ### Bounds on suprema -/

lemma exists_eq_of_iSup_eq_of_not_isSuccPrelimit
    {Œπ : Type u} (f : Œπ ‚Üí Cardinal.{v}) (œâ : Cardinal.{v})
    (hœâ : ¬¨ IsSuccPrelimit œâ)
    (h : ‚®Ü i : Œπ, f i = œâ) : ‚àÉ i, f i = œâ := by
  subst h
  suffices BddAbove (range f) from (isLUB_csSup' this).mem_of_not_isSuccPrelimit hœâ
  contrapose! hœâ with hf
  rw [iSup, csSup_of_not_bddAbove hf, csSup_empty]
  exact isSuccPrelimit_bot

lemma exists_eq_of_iSup_eq_of_not_isSuccLimit
    {Œπ : Type u} [hŒπ : Nonempty Œπ] (f : Œπ ‚Üí Cardinal.{v}) (hf : BddAbove (range f))
    {c : Cardinal.{v}} (hc : ¬¨ IsSuccLimit c)
    (h : ‚®Ü i, f i = c) : ‚àÉ i, f i = c := by
  rw [Cardinal.isSuccLimit_iff] at hc
  refine (not_and_or.mp hc).elim (fun e ‚Ü¶ ‚ü®hŒπ.some, ?_‚ü©)
    (Cardinal.exists_eq_of_iSup_eq_of_not_isSuccPrelimit.{u, v} f c ¬∑ h)
  cases not_not.mp e
  rw [‚Üê le_zero_iff] at h ‚ä¢
  exact (le_ciSup hf _).trans h

/-! ### Indexed cardinal `prod` -/

/-- **K√∂nig's theorem** -/
theorem sum_lt_prod {Œπ} (f g : Œπ ‚Üí Cardinal) (H : ‚àÄ i, f i < g i) : sum f < prod g :=
  lt_of_not_ge fun ‚ü®F‚ü© => by
    have : Inhabited (‚àÄ i : Œπ, (g i).out) := by
      refine ‚ü®fun i => Classical.choice <| mk_ne_zero_iff.1 ?_‚ü©
      rw [mk_out]
      exact (H i).ne_bot
    let G := invFun F
    have sG : Surjective G := invFun_surjective F.2
    choose C hc using
      show ‚àÄ i, ‚àÉ b, ‚àÄ a, G ‚ü®i, a‚ü© i ‚â† b by
        intro i
        simp only [not_exists.symm, not_forall.symm]
        refine fun h => (H i).not_ge ?_
        rw [‚Üê mk_out (f i), ‚Üê mk_out (g i)]
        exact ‚ü®Embedding.ofSurjective _ h‚ü©
    let ‚ü®‚ü®i, a‚ü©, h‚ü© := sG C
    exact hc i a (congr_fun h _)

theorem prod_le_prod {Œπ} (f g : Œπ ‚Üí Cardinal) (H : ‚àÄ i, f i ‚â§ g i) : prod f ‚â§ prod g :=
  ‚ü®Embedding.piCongrRight fun i =>
      Classical.choice <| by have := H i; rwa [‚Üê mk_out (f i), ‚Üê mk_out (g i)] at this‚ü©

/-! ### The first infinite cardinal `aleph0` -/

theorem aleph0_pos : 0 < ‚Ñµ‚ÇÄ :=
  pos_iff_ne_zero.2 aleph0_ne_zero

@[simp]
theorem aleph0_le_lift {c : Cardinal.{u}} : ‚Ñµ‚ÇÄ ‚â§ lift.{v} c ‚Üî ‚Ñµ‚ÇÄ ‚â§ c := by
  simpa using lift_le (a := ‚Ñµ‚ÇÄ)

@[simp]
theorem lift_le_aleph0 {c : Cardinal.{u}} : lift.{v} c ‚â§ ‚Ñµ‚ÇÄ ‚Üî c ‚â§ ‚Ñµ‚ÇÄ := by
  simpa using lift_le (b := ‚Ñµ‚ÇÄ)

@[simp]
theorem aleph0_lt_lift {c : Cardinal.{u}} : ‚Ñµ‚ÇÄ < lift.{v} c ‚Üî ‚Ñµ‚ÇÄ < c := by
  simpa using lift_lt (a := ‚Ñµ‚ÇÄ)

@[simp]
theorem lift_lt_aleph0 {c : Cardinal.{u}} : lift.{v} c < ‚Ñµ‚ÇÄ ‚Üî c < ‚Ñµ‚ÇÄ := by
  simpa using lift_lt (b := ‚Ñµ‚ÇÄ)

@[simp]
theorem aleph0_eq_lift {c : Cardinal.{u}} : ‚Ñµ‚ÇÄ = lift.{v} c ‚Üî ‚Ñµ‚ÇÄ = c := by
  simpa using lift_inj (a := ‚Ñµ‚ÇÄ)

@[simp]
theorem lift_eq_aleph0 {c : Cardinal.{u}} : lift.{v} c = ‚Ñµ‚ÇÄ ‚Üî c = ‚Ñµ‚ÇÄ := by
  simpa using lift_inj (b := ‚Ñµ‚ÇÄ)

/-! ### Properties about the cast from `‚Ñï` -/

theorem mk_fin (n : ‚Ñï) : #(Fin n) = n := by simp

@[simp]
theorem lift_natCast (n : ‚Ñï) : lift.{u} (n : Cardinal.{v}) = n := by induction n <;> simp [*]

@[simp]
theorem lift_ofNat (n : ‚Ñï) [n.AtLeastTwo] :
    lift.{u} (ofNat(n) : Cardinal.{v}) = OfNat.ofNat n :=
  lift_natCast n

@[simp]
theorem lift_eq_nat_iff {a : Cardinal.{u}} {n : ‚Ñï} : lift.{v} a = n ‚Üî a = n :=
  lift_injective.eq_iff' (lift_natCast n)

@[simp]
theorem lift_eq_ofNat_iff {a : Cardinal.{u}} {n : ‚Ñï} [n.AtLeastTwo] :
    lift.{v} a = ofNat(n) ‚Üî a = OfNat.ofNat n :=
  lift_eq_nat_iff

@[simp]
theorem nat_eq_lift_iff {n : ‚Ñï} {a : Cardinal.{u}} :
    (n : Cardinal) = lift.{v} a ‚Üî (n : Cardinal) = a := by
  rw [‚Üê lift_natCast.{v,u} n, lift_inj]

@[simp]
theorem zero_eq_lift_iff {a : Cardinal.{u}} :
    (0 : Cardinal) = lift.{v} a ‚Üî 0 = a := by
  simpa using nat_eq_lift_iff (n := 0)

@[simp]
theorem one_eq_lift_iff {a : Cardinal.{u}} :
    (1 : Cardinal) = lift.{v} a ‚Üî 1 = a := by
  simpa using nat_eq_lift_iff (n := 1)

@[simp]
theorem ofNat_eq_lift_iff {a : Cardinal.{u}} {n : ‚Ñï} [n.AtLeastTwo] :
    (ofNat(n) : Cardinal) = lift.{v} a ‚Üî (OfNat.ofNat n : Cardinal) = a :=
  nat_eq_lift_iff

@[simp]
theorem lift_le_nat_iff {a : Cardinal.{u}} {n : ‚Ñï} : lift.{v} a ‚â§ n ‚Üî a ‚â§ n := by
  rw [‚Üê lift_natCast.{v,u}, lift_le]

@[simp]
theorem lift_le_one_iff {a : Cardinal.{u}} :
    lift.{v} a ‚â§ 1 ‚Üî a ‚â§ 1 := by
  simpa using lift_le_nat_iff (n := 1)

@[simp]
theorem lift_le_ofNat_iff {a : Cardinal.{u}} {n : ‚Ñï} [n.AtLeastTwo] :
    lift.{v} a ‚â§ ofNat(n) ‚Üî a ‚â§ OfNat.ofNat n :=
  lift_le_nat_iff

@[simp]
theorem nat_le_lift_iff {n : ‚Ñï} {a : Cardinal.{u}} : n ‚â§ lift.{v} a ‚Üî n ‚â§ a := by
  rw [‚Üê lift_natCast.{v,u}, lift_le]

@[simp]
theorem one_le_lift_iff {a : Cardinal.{u}} :
    (1 : Cardinal) ‚â§ lift.{v} a ‚Üî 1 ‚â§ a := by
  simpa using nat_le_lift_iff (n := 1)

@[simp]
theorem ofNat_le_lift_iff {a : Cardinal.{u}} {n : ‚Ñï} [n.AtLeastTwo] :
    (ofNat(n) : Cardinal) ‚â§ lift.{v} a ‚Üî (OfNat.ofNat n : Cardinal) ‚â§ a :=
  nat_le_lift_iff

@[simp]
theorem lift_lt_nat_iff {a : Cardinal.{u}} {n : ‚Ñï} : lift.{v} a < n ‚Üî a < n := by
  rw [‚Üê lift_natCast.{v,u}, lift_lt]

@[simp]
theorem lift_lt_ofNat_iff {a : Cardinal.{u}} {n : ‚Ñï} [n.AtLeastTwo] :
    lift.{v} a < ofNat(n) ‚Üî a < OfNat.ofNat n :=
  lift_lt_nat_iff

@[simp]
theorem nat_lt_lift_iff {n : ‚Ñï} {a : Cardinal.{u}} : n < lift.{v} a ‚Üî n < a := by
  rw [‚Üê lift_natCast.{v,u}, lift_lt]

@[simp]
theorem zero_lt_lift_iff {a : Cardinal.{u}} :
    (0 : Cardinal) < lift.{v} a ‚Üî 0 < a := by
  simpa using nat_lt_lift_iff (n := 0)

@[simp]
theorem one_lt_lift_iff {a : Cardinal.{u}} :
    (1 : Cardinal) < lift.{v} a ‚Üî 1 < a := by
  simpa using nat_lt_lift_iff (n := 1)

@[simp]
theorem ofNat_lt_lift_iff {a : Cardinal.{u}} {n : ‚Ñï} [n.AtLeastTwo] :
    (ofNat(n) : Cardinal) < lift.{v} a ‚Üî (OfNat.ofNat n : Cardinal) < a :=
  nat_lt_lift_iff

theorem mk_coe_finset {Œ± : Type u} {s : Finset Œ±} : #s = ‚Üë(Finset.card s) := by simp

theorem card_le_of_finset {Œ±} (s : Finset Œ±) : (s.card : Cardinal) ‚â§ #Œ± :=
  @mk_coe_finset _ s ‚ñ∏ mk_set_le _

instance : CharZero Cardinal := by
  refine ‚ü®fun a b h ‚Ü¶ ?_‚ü©
  rwa [‚Üê lift_mk_fin, ‚Üê lift_mk_fin, lift_inj, Cardinal.eq, ‚Üê Fintype.card_eq,
    Fintype.card_fin, Fintype.card_fin] at h

end Cardinal
