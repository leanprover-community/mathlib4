/-
Copyright (c) 2017 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Floris van Doorn, Violeta Hern√°ndez Palacios
-/
import Mathlib.SetTheory.Cardinal.Ordinal
import Mathlib.SetTheory.Ordinal.FixedPoint

/-!
# Cofinality

This file contains the definition of cofinality of an ordinal number and regular cardinals

## Main Definitions

* `Ordinal.cof o` is the cofinality of the ordinal `o`.
  If `o` is the order type of the relation `<` on `Œ±`, then `o.cof` is the smallest cardinality of a
  subset `s` of Œ± that is *cofinal* in `Œ±`, i.e. `‚àÄ x : Œ±, ‚àÉ y ‚àà s, ¬¨ y < x`.
* `Cardinal.IsStrongLimit c` means that `c` is a strong limit cardinal:
  `c ‚â† 0 ‚àß ‚àÄ x < c, 2 ^ x < c`.
* `Cardinal.IsRegular c` means that `c` is a regular cardinal: `‚Ñµ‚ÇÄ ‚â§ c ‚àß c.ord.cof = c`.
* `Cardinal.IsInaccessible c` means that `c` is strongly inaccessible:
  `‚Ñµ‚ÇÄ < c ‚àß IsRegular c ‚àß IsStrongLimit c`.

## Main Statements

* `Ordinal.infinite_pigeonhole_card`: the infinite pigeonhole principle
* `Cardinal.lt_power_cof`: A consequence of K√∂nig's theorem stating that `c < c ^ c.ord.cof` for
  `c ‚â• ‚Ñµ‚ÇÄ`
* `Cardinal.univ_inaccessible`: The type of ordinals in `Type u` form an inaccessible cardinal
  (in `Type v` with `v > u`). This shows (externally) that in `Type u` there are at least `u`
  inaccessible cardinals.

## Implementation Notes

* The cofinality is defined for ordinals.
  If `c` is a cardinal number, its cofinality is `c.ord.cof`.

## Tags

cofinality, regular cardinals, limits cardinals, inaccessible cardinals,
infinite pigeonhole principle
-/

noncomputable section

open Function Cardinal Set Order
open scoped Ordinal

universe u v w

variable {Œ± : Type*} {r : Œ± ‚Üí Œ± ‚Üí Prop}

/-! ### Cofinality of orders -/


namespace Order

/-- Cofinality of a reflexive order `‚âº`. This is the smallest cardinality
  of a subset `S : Set Œ±` such that `‚àÄ a, ‚àÉ b ‚àà S, a ‚âº b`. -/
def cof (r : Œ± ‚Üí Œ± ‚Üí Prop) : Cardinal :=
  sInf { c | ‚àÉ S : Set Œ±, (‚àÄ a, ‚àÉ b ‚àà S, r a b) ‚àß #S = c }

/-- The set in the definition of `Order.cof` is nonempty. -/
theorem cof_nonempty (r : Œ± ‚Üí Œ± ‚Üí Prop) [IsRefl Œ± r] :
    { c | ‚àÉ S : Set Œ±, (‚àÄ a, ‚àÉ b ‚àà S, r a b) ‚àß #S = c }.Nonempty :=
  ‚ü®_, Set.univ, fun a => ‚ü®a, ‚ü®‚ü©, refl _‚ü©, rfl‚ü©

theorem cof_le (r : Œ± ‚Üí Œ± ‚Üí Prop) {S : Set Œ±} (h : ‚àÄ a, ‚àÉ b ‚àà S, r a b) : cof r ‚â§ #S :=
  csInf_le' ‚ü®S, h, rfl‚ü©

theorem le_cof {r : Œ± ‚Üí Œ± ‚Üí Prop} [IsRefl Œ± r] (c : Cardinal) :
    c ‚â§ cof r ‚Üî ‚àÄ {S : Set Œ±}, (‚àÄ a, ‚àÉ b ‚àà S, r a b) ‚Üí c ‚â§ #S := by
  rw [cof, le_csInf_iff'' (cof_nonempty r)]
  use fun H S h => H _ ‚ü®S, h, rfl‚ü©
  rintro H d ‚ü®S, h, rfl‚ü©
  exact H h

end Order

theorem RelIso.cof_le_lift {Œ± : Type u} {Œ≤ : Type v} {r : Œ± ‚Üí Œ± ‚Üí Prop} {s} [IsRefl Œ≤ s]
    (f : r ‚âÉr s) : Cardinal.lift.{max u v} (Order.cof r) ‚â§
    Cardinal.lift.{max u v} (Order.cof s) := by
  rw [Order.cof, Order.cof, lift_sInf, lift_sInf,
    le_csInf_iff'' ((Order.cof_nonempty s).image _)]
  rintro - ‚ü®-, ‚ü®u, H, rfl‚ü©, rfl‚ü©
  apply csInf_le'
  refine
    ‚ü®_, ‚ü®f.symm '' u, fun a => ?_, rfl‚ü©,
      lift_mk_eq.{u, v, max u v}.2 ‚ü®(f.symm.toEquiv.image u).symm‚ü©‚ü©
  rcases H (f a) with ‚ü®b, hb, hb'‚ü©
  refine ‚ü®f.symm b, mem_image_of_mem _ hb, f.map_rel_iff.1 ?_‚ü©
  rwa [RelIso.apply_symm_apply]

theorem RelIso.cof_eq_lift {Œ± : Type u} {Œ≤ : Type v} {r s} [IsRefl Œ± r] [IsRefl Œ≤ s] (f : r ‚âÉr s) :
    Cardinal.lift.{max u v} (Order.cof r) = Cardinal.lift.{max u v} (Order.cof s) :=
  (RelIso.cof_le_lift f).antisymm (RelIso.cof_le_lift f.symm)

theorem RelIso.cof_le {Œ± Œ≤ : Type u} {r : Œ± ‚Üí Œ± ‚Üí Prop} {s} [IsRefl Œ≤ s] (f : r ‚âÉr s) :
    Order.cof r ‚â§ Order.cof s :=
  lift_le.1 (RelIso.cof_le_lift f)

theorem RelIso.cof_eq {Œ± Œ≤ : Type u} {r s} [IsRefl Œ± r] [IsRefl Œ≤ s] (f : r ‚âÉr s) :
    Order.cof r = Order.cof s :=
  lift_inj.1 (RelIso.cof_eq_lift f)

/-- Cofinality of a strict order `‚â∫`. This is the smallest cardinality of a set `S : Set Œ±` such
that `‚àÄ a, ‚àÉ b ‚àà S, ¬¨ b ‚â∫ a`. -/
def StrictOrder.cof (r : Œ± ‚Üí Œ± ‚Üí Prop) : Cardinal :=
  Order.cof (swap r·∂ú)

/-- The set in the definition of `Order.StrictOrder.cof` is nonempty. -/
theorem StrictOrder.cof_nonempty (r : Œ± ‚Üí Œ± ‚Üí Prop) [IsIrrefl Œ± r] :
    { c | ‚àÉ S : Set Œ±, Unbounded r S ‚àß #S = c }.Nonempty :=
  @Order.cof_nonempty Œ± _ (IsRefl.swap r·∂ú)

/-! ### Cofinality of ordinals -/


namespace Ordinal

/-- Cofinality of an ordinal. This is the smallest cardinal of a
  subset `S` of the ordinal which is unbounded, in the sense
  `‚àÄ a, ‚àÉ b ‚àà S, a ‚â§ b`. It is defined for all ordinals, but
  `cof 0 = 0` and `cof (succ o) = 1`, so it is only really
  interesting on limit ordinals (when it is an infinite cardinal). -/
def cof (o : Ordinal.{u}) : Cardinal.{u} :=
  o.liftOn (fun a => StrictOrder.cof a.r)
    (by
      rintro ‚ü®Œ±, r, wo‚ÇÅ‚ü© ‚ü®Œ≤, s, wo‚ÇÇ‚ü© ‚ü®‚ü®f, hf‚ü©‚ü©
      haveI := wo‚ÇÅ; haveI := wo‚ÇÇ
      dsimp only
      apply @RelIso.cof_eq _ _ _ _ ?_ ?_
      ¬∑ constructor
        exact @fun a b => not_iff_not.2 hf
      ¬∑ dsimp only [swap]
        exact ‚ü®fun _ => irrefl _‚ü©
      ¬∑ dsimp only [swap]
        exact ‚ü®fun _ => irrefl _‚ü©)

theorem cof_type (r : Œ± ‚Üí Œ± ‚Üí Prop) [IsWellOrder Œ± r] : (type r).cof = StrictOrder.cof r :=
  rfl

theorem le_cof_type [IsWellOrder Œ± r] {c} : c ‚â§ cof (type r) ‚Üî ‚àÄ S, Unbounded r S ‚Üí c ‚â§ #S :=
  (le_csInf_iff'' (StrictOrder.cof_nonempty r)).trans
    ‚ü®fun H S h => H _ ‚ü®S, h, rfl‚ü©, by
      rintro H d ‚ü®S, h, rfl‚ü©
      exact H _ h‚ü©

theorem cof_type_le [IsWellOrder Œ± r] {S : Set Œ±} (h : Unbounded r S) : cof (type r) ‚â§ #S :=
  le_cof_type.1 le_rfl S h

theorem lt_cof_type [IsWellOrder Œ± r] {S : Set Œ±} : #S < cof (type r) ‚Üí Bounded r S := by
  simpa using not_imp_not.2 cof_type_le

theorem cof_eq (r : Œ± ‚Üí Œ± ‚Üí Prop) [IsWellOrder Œ± r] : ‚àÉ S, Unbounded r S ‚àß #S = cof (type r) :=
  csInf_mem (StrictOrder.cof_nonempty r)

theorem ord_cof_eq (r : Œ± ‚Üí Œ± ‚Üí Prop) [IsWellOrder Œ± r] :
    ‚àÉ S, Unbounded r S ‚àß type (Subrel r S) = (cof (type r)).ord := by
  let ‚ü®S, hS, e‚ü© := cof_eq r
  let ‚ü®s, _, e'‚ü© := Cardinal.ord_eq S
  let T : Set Œ± := { a | ‚àÉ aS : a ‚àà S, ‚àÄ b : S, s b ‚ü®_, aS‚ü© ‚Üí r b a }
  suffices Unbounded r T by
    refine ‚ü®T, this, le_antisymm ?_ (Cardinal.ord_le.2 <| cof_type_le this)‚ü©
    rw [‚Üê e, e']
    refine
      (RelEmbedding.ofMonotone
          (fun a : T =>
            (‚ü®a,
                let ‚ü®aS, _‚ü© := a.2
                aS‚ü© :
              S))
          fun a b h => ?_).ordinal_type_le
    rcases a with ‚ü®a, aS, ha‚ü©
    rcases b with ‚ü®b, bS, hb‚ü©
    change s ‚ü®a, _‚ü© ‚ü®b, _‚ü©
    refine ((trichotomous_of s _ _).resolve_left fun hn => ?_).resolve_left ?_
    ¬∑ exact asymm h (ha _ hn)
    ¬∑ intro e
      injection e with e
      subst b
      exact irrefl _ h
  intro a
  have : { b : S | ¬¨r b a }.Nonempty :=
    let ‚ü®b, bS, ba‚ü© := hS a
    ‚ü®‚ü®b, bS‚ü©, ba‚ü©
  let b := (IsWellFounded.wf : WellFounded s).min _ this
  have ba : ¬¨r b a := IsWellFounded.wf.min_mem _ this
  refine ‚ü®b, ‚ü®b.2, fun c => not_imp_not.1 fun h => ?_‚ü©, ba‚ü©
  rw [show ‚àÄ b : S, (‚ü®b, b.2‚ü© : S) = b by intro b; cases b; rfl]
  exact IsWellFounded.wf.not_lt_min _ this (IsOrderConnected.neg_trans h ba)

/-! ### Cofinality of suprema and least strict upper bounds -/


private theorem card_mem_cof {o} : ‚àÉ (Œπ : _) (f : Œπ ‚Üí Ordinal), lsub.{u, u} f = o ‚àß #Œπ = o.card :=
  ‚ü®_, _, lsub_typein o, mk_toType o‚ü©

/-- The set in the `lsub` characterization of `cof` is nonempty. -/
theorem cof_lsub_def_nonempty (o) :
    { a : Cardinal | ‚àÉ (Œπ : _) (f : Œπ ‚Üí Ordinal), lsub.{u, u} f = o ‚àß #Œπ = a }.Nonempty :=
  ‚ü®_, card_mem_cof‚ü©

theorem cof_eq_sInf_lsub (o : Ordinal.{u}) : cof o =
    sInf { a : Cardinal | ‚àÉ (Œπ : Type u) (f : Œπ ‚Üí Ordinal), lsub.{u, u} f = o ‚àß #Œπ = a } := by
  refine le_antisymm (le_csInf (cof_lsub_def_nonempty o) ?_) (csInf_le' ?_)
  ¬∑ rintro a ‚ü®Œπ, f, hf, rfl‚ü©
    rw [‚Üê type_lt o]
    refine
      (cof_type_le fun a => ?_).trans
        (@mk_le_of_injective _ _
          (fun s : typein ((¬∑ < ¬∑) : o.toType ‚Üí o.toType ‚Üí Prop) ‚Åª¬π' Set.range f =>
            Classical.choose s.prop)
          fun s t hst => by
          let H := congr_arg f hst
          rwa [Classical.choose_spec s.prop, Classical.choose_spec t.prop, typein_inj,
            Subtype.coe_inj] at H)
    have := typein_lt_self a
    simp_rw [‚Üê hf, lt_lsub_iff] at this
    cases' this with i hi
    refine ‚ü®enum (Œ± := o.toType) (¬∑ < ¬∑) ‚ü®f i, ?_‚ü©, ?_, ?_‚ü©
    ¬∑ rw [type_lt, ‚Üê hf]
      apply lt_lsub
    ¬∑ rw [mem_preimage, typein_enum]
      exact mem_range_self i
    ¬∑ rwa [‚Üê typein_le_typein, typein_enum]
  ¬∑ rcases cof_eq (Œ± := o.toType) (¬∑ < ¬∑) with ‚ü®S, hS, hS'‚ü©
    let f : S ‚Üí Ordinal := fun s => typein LT.lt s.val
    refine ‚ü®S, f, le_antisymm (lsub_le fun i => typein_lt_self (o := o) i)
      (le_of_forall_lt fun a ha => ?_), by rwa [type_lt o] at hS'‚ü©
    rw [‚Üê type_lt o] at ha
    rcases hS (enum (¬∑ < ¬∑) ‚ü®a, ha‚ü©) with ‚ü®b, hb, hb'‚ü©
    rw [‚Üê typein_le_typein, typein_enum] at hb'
    exact hb'.trans_lt (lt_lsub.{u, u} f ‚ü®b, hb‚ü©)

@[simp]
theorem lift_cof (o) : Cardinal.lift.{u, v} (cof o) = cof (Ordinal.lift.{u, v} o) := by
  refine inductionOn o ?_
  intro Œ± r _
  apply le_antisymm
  ¬∑ refine le_cof_type.2 fun S H => ?_
    have : Cardinal.lift.{u, v} #(ULift.up ‚Åª¬π' S) ‚â§ #(S : Type (max u v)) := by
      rw [‚Üê Cardinal.lift_umax.{v, u}, ‚Üê Cardinal.lift_id'.{v, u} #S]
      exact mk_preimage_of_injective_lift.{v, max u v} ULift.up S (ULift.up_injective.{v, u})
    refine (Cardinal.lift_le.2 <| cof_type_le ?_).trans this
    exact fun a =>
      let ‚ü®‚ü®b‚ü©, bs, br‚ü© := H ‚ü®a‚ü©
      ‚ü®b, bs, br‚ü©
  ¬∑ rcases cof_eq r with ‚ü®S, H, e'‚ü©
    have : #(ULift.down.{u, v} ‚Åª¬π' S) ‚â§ Cardinal.lift.{u, v} #S :=
      ‚ü®‚ü®fun ‚ü®‚ü®x‚ü©, h‚ü© => ‚ü®‚ü®x, h‚ü©‚ü©, fun ‚ü®‚ü®x‚ü©, h‚ÇÅ‚ü© ‚ü®‚ü®y‚ü©, h‚ÇÇ‚ü© e => by
          simp at e; congr‚ü©‚ü©
    rw [e'] at this
    refine (cof_type_le ?_).trans this
    exact fun ‚ü®a‚ü© =>
      let ‚ü®b, bs, br‚ü© := H a
      ‚ü®‚ü®b‚ü©, bs, br‚ü©

theorem cof_le_card (o) : cof o ‚â§ card o := by
  rw [cof_eq_sInf_lsub]
  exact csInf_le' card_mem_cof

theorem cof_ord_le (c : Cardinal) : c.ord.cof ‚â§ c := by simpa using cof_le_card c.ord

theorem ord_cof_le (o : Ordinal.{u}) : o.cof.ord ‚â§ o :=
  (ord_le_ord.2 (cof_le_card o)).trans (ord_card_le o)

theorem exists_lsub_cof (o : Ordinal) :
    ‚àÉ (Œπ : _) (f : Œπ ‚Üí Ordinal), lsub.{u, u} f = o ‚àß #Œπ = cof o := by
  rw [cof_eq_sInf_lsub]
  exact csInf_mem (cof_lsub_def_nonempty o)

theorem cof_lsub_le {Œπ} (f : Œπ ‚Üí Ordinal) : cof (lsub.{u, u} f) ‚â§ #Œπ := by
  rw [cof_eq_sInf_lsub]
  exact csInf_le' ‚ü®Œπ, f, rfl, rfl‚ü©

theorem cof_lsub_le_lift {Œπ} (f : Œπ ‚Üí Ordinal) :
    cof (lsub.{u, v} f) ‚â§ Cardinal.lift.{v, u} #Œπ := by
  rw [‚Üê mk_uLift.{u, v}]
  convert cof_lsub_le.{max u v} fun i : ULift.{v, u} Œπ => f i.down
  exact
    lsub_eq_of_range_eq.{u, max u v, max u v}
      (Set.ext fun x => ‚ü®fun ‚ü®i, hi‚ü© => ‚ü®ULift.up.{v, u} i, hi‚ü©, fun ‚ü®i, hi‚ü© => ‚ü®_, hi‚ü©‚ü©)

theorem le_cof_iff_lsub {o : Ordinal} {a : Cardinal} :
    a ‚â§ cof o ‚Üî ‚àÄ {Œπ} (f : Œπ ‚Üí Ordinal), lsub.{u, u} f = o ‚Üí a ‚â§ #Œπ := by
  rw [cof_eq_sInf_lsub]
  exact
    (le_csInf_iff'' (cof_lsub_def_nonempty o)).trans
      ‚ü®fun H Œπ f hf => H _ ‚ü®Œπ, f, hf, rfl‚ü©, fun H b ‚ü®Œπ, f, hf, hb‚ü© => by
        rw [‚Üê hb]
        exact H _ hf‚ü©

theorem lsub_lt_ord_lift {Œπ} {f : Œπ ‚Üí Ordinal} {c : Ordinal}
    (hŒπ : Cardinal.lift.{v, u} #Œπ < c.cof)
    (hf : ‚àÄ i, f i < c) : lsub.{u, v} f < c :=
  lt_of_le_of_ne (lsub_le hf) fun h => by
    subst h
    exact (cof_lsub_le_lift.{u, v} f).not_lt hŒπ

theorem lsub_lt_ord {Œπ} {f : Œπ ‚Üí Ordinal} {c : Ordinal} (hŒπ : #Œπ < c.cof) :
    (‚àÄ i, f i < c) ‚Üí lsub.{u, u} f < c :=
  lsub_lt_ord_lift (by rwa [(#Œπ).lift_id])

theorem cof_sup_le_lift {Œπ} {f : Œπ ‚Üí Ordinal} (H : ‚àÄ i, f i < sup.{u, v} f) :
    cof (sup.{u, v} f) ‚â§ Cardinal.lift.{v, u} #Œπ := by
  rw [‚Üê sup_eq_lsub_iff_lt_sup.{u, v}] at H
  rw [H]
  exact cof_lsub_le_lift f

theorem cof_sup_le {Œπ} {f : Œπ ‚Üí Ordinal} (H : ‚àÄ i, f i < sup.{u, u} f) :
    cof (sup.{u, u} f) ‚â§ #Œπ := by
  rw [‚Üê (#Œπ).lift_id]
  exact cof_sup_le_lift H

theorem sup_lt_ord_lift {Œπ} {f : Œπ ‚Üí Ordinal} {c : Ordinal} (hŒπ : Cardinal.lift.{v, u} #Œπ < c.cof)
    (hf : ‚àÄ i, f i < c) : sup.{u, v} f < c :=
  (sup_le_lsub.{u, v} f).trans_lt (lsub_lt_ord_lift hŒπ hf)

theorem sup_lt_ord {Œπ} {f : Œπ ‚Üí Ordinal} {c : Ordinal} (hŒπ : #Œπ < c.cof) :
    (‚àÄ i, f i < c) ‚Üí sup.{u, u} f < c :=
  sup_lt_ord_lift (by rwa [(#Œπ).lift_id])

theorem iSup_lt_lift {Œπ} {f : Œπ ‚Üí Cardinal} {c : Cardinal}
    (hŒπ : Cardinal.lift.{v, u} #Œπ < c.ord.cof)
    (hf : ‚àÄ i, f i < c) : iSup.{max u v + 1, u + 1} f < c := by
  rw [‚Üê ord_lt_ord, iSup_ord (Cardinal.bddAbove_range.{u, v} _)]
  refine sup_lt_ord_lift hŒπ fun i => ?_
  rw [ord_lt_ord]
  apply hf

theorem iSup_lt {Œπ} {f : Œπ ‚Üí Cardinal} {c : Cardinal} (hŒπ : #Œπ < c.ord.cof) :
    (‚àÄ i, f i < c) ‚Üí iSup f < c :=
  iSup_lt_lift (by rwa [(#Œπ).lift_id])

theorem nfpFamily_lt_ord_lift {Œπ} {f : Œπ ‚Üí Ordinal ‚Üí Ordinal} {c} (hc : ‚Ñµ‚ÇÄ < cof c)
    (hc' : Cardinal.lift.{v, u} #Œπ < cof c) (hf : ‚àÄ (i), ‚àÄ b < c, f i b < c) {a} (ha : a < c) :
    nfpFamily.{u, v} f a < c := by
  refine sup_lt_ord_lift ((Cardinal.lift_le.2 (mk_list_le_max Œπ)).trans_lt ?_) fun l => ?_
  ¬∑ rw [lift_max]
    apply max_lt _ hc'
    rwa [Cardinal.lift_aleph0]
  ¬∑ induction' l with i l H
    ¬∑ exact ha
    ¬∑ exact hf _ _ H

theorem nfpFamily_lt_ord {Œπ} {f : Œπ ‚Üí Ordinal ‚Üí Ordinal} {c} (hc : ‚Ñµ‚ÇÄ < cof c) (hc' : #Œπ < cof c)
    (hf : ‚àÄ (i), ‚àÄ b < c, f i b < c) {a} : a < c ‚Üí nfpFamily.{u, u} f a < c :=
  nfpFamily_lt_ord_lift hc (by rwa [(#Œπ).lift_id]) hf

theorem nfpBFamily_lt_ord_lift {o : Ordinal} {f : ‚àÄ a < o, Ordinal ‚Üí Ordinal} {c} (hc : ‚Ñµ‚ÇÄ < cof c)
    (hc' : Cardinal.lift.{v, u} o.card < cof c) (hf : ‚àÄ (i hi), ‚àÄ b < c, f i hi b < c) {a} :
    a < c ‚Üí nfpBFamily.{u, v} o f a < c :=
  nfpFamily_lt_ord_lift hc (by rwa [mk_toType]) fun i => hf _ _

theorem nfpBFamily_lt_ord {o : Ordinal} {f : ‚àÄ a < o, Ordinal ‚Üí Ordinal} {c} (hc : ‚Ñµ‚ÇÄ < cof c)
    (hc' : o.card < cof c) (hf : ‚àÄ (i hi), ‚àÄ b < c, f i hi b < c) {a} :
    a < c ‚Üí nfpBFamily.{u, u} o f a < c :=
  nfpBFamily_lt_ord_lift hc (by rwa [o.card.lift_id]) hf

theorem nfp_lt_ord {f : Ordinal ‚Üí Ordinal} {c} (hc : ‚Ñµ‚ÇÄ < cof c) (hf : ‚àÄ i < c, f i < c) {a} :
    a < c ‚Üí nfp f a < c :=
  nfpFamily_lt_ord_lift hc (by simpa using Cardinal.one_lt_aleph0.trans hc) fun _ => hf

theorem exists_blsub_cof (o : Ordinal) :
    ‚àÉ f : ‚àÄ a < (cof o).ord, Ordinal, blsub.{u, u} _ f = o := by
  rcases exists_lsub_cof o with ‚ü®Œπ, f, hf, hŒπ‚ü©
  rcases Cardinal.ord_eq Œπ with ‚ü®r, hr, hŒπ'‚ü©
  rw [‚Üê @blsub_eq_lsub' Œπ r hr] at hf
  rw [‚Üê hŒπ, hŒπ']
  exact ‚ü®_, hf‚ü©

theorem le_cof_iff_blsub {b : Ordinal} {a : Cardinal} :
    a ‚â§ cof b ‚Üî ‚àÄ {o} (f : ‚àÄ a < o, Ordinal), blsub.{u, u} o f = b ‚Üí a ‚â§ o.card :=
  le_cof_iff_lsub.trans
    ‚ü®fun H o f hf => by simpa using H _ hf, fun H Œπ f hf => by
      rcases Cardinal.ord_eq Œπ with ‚ü®r, hr, hŒπ'‚ü©
      rw [‚Üê @blsub_eq_lsub' Œπ r hr] at hf
      simpa using H _ hf‚ü©

theorem cof_blsub_le_lift {o} (f : ‚àÄ a < o, Ordinal) :
    cof (blsub.{u, v} o f) ‚â§ Cardinal.lift.{v, u} o.card := by
  rw [‚Üê mk_toType o]
  exact cof_lsub_le_lift _

theorem cof_blsub_le {o} (f : ‚àÄ a < o, Ordinal) : cof (blsub.{u, u} o f) ‚â§ o.card := by
  rw [‚Üê o.card.lift_id]
  exact cof_blsub_le_lift f

theorem blsub_lt_ord_lift {o : Ordinal.{u}} {f : ‚àÄ a < o, Ordinal} {c : Ordinal}
    (ho : Cardinal.lift.{v, u} o.card < c.cof) (hf : ‚àÄ i hi, f i hi < c) : blsub.{u, v} o f < c :=
  lt_of_le_of_ne (blsub_le hf) fun h =>
    ho.not_le (by simpa [‚Üê iSup_ord, hf, h] using cof_blsub_le_lift.{u, v} f)

theorem blsub_lt_ord {o : Ordinal} {f : ‚àÄ a < o, Ordinal} {c : Ordinal} (ho : o.card < c.cof)
    (hf : ‚àÄ i hi, f i hi < c) : blsub.{u, u} o f < c :=
  blsub_lt_ord_lift (by rwa [o.card.lift_id]) hf

theorem cof_bsup_le_lift {o : Ordinal} {f : ‚àÄ a < o, Ordinal} (H : ‚àÄ i h, f i h < bsup.{u, v} o f) :
    cof (bsup.{u, v} o f) ‚â§ Cardinal.lift.{v, u} o.card := by
  rw [‚Üê bsup_eq_blsub_iff_lt_bsup.{u, v}] at H
  rw [H]
  exact cof_blsub_le_lift.{u, v} f

theorem cof_bsup_le {o : Ordinal} {f : ‚àÄ a < o, Ordinal} :
    (‚àÄ i h, f i h < bsup.{u, u} o f) ‚Üí cof (bsup.{u, u} o f) ‚â§ o.card := by
  rw [‚Üê o.card.lift_id]
  exact cof_bsup_le_lift

theorem bsup_lt_ord_lift {o : Ordinal} {f : ‚àÄ a < o, Ordinal} {c : Ordinal}
    (ho : Cardinal.lift.{v, u} o.card < c.cof) (hf : ‚àÄ i hi, f i hi < c) : bsup.{u, v} o f < c :=
  (bsup_le_blsub f).trans_lt (blsub_lt_ord_lift ho hf)

theorem bsup_lt_ord {o : Ordinal} {f : ‚àÄ a < o, Ordinal} {c : Ordinal} (ho : o.card < c.cof) :
    (‚àÄ i hi, f i hi < c) ‚Üí bsup.{u, u} o f < c :=
  bsup_lt_ord_lift (by rwa [o.card.lift_id])

/-! ### Basic results -/


@[simp]
theorem cof_zero : cof 0 = 0 := by
  refine LE.le.antisymm  ?_ (Cardinal.zero_le _)
  rw [‚Üê card_zero]
  exact cof_le_card 0

@[simp]
theorem cof_eq_zero {o} : cof o = 0 ‚Üî o = 0 :=
  ‚ü®inductionOn o fun Œ± r _ z =>
      let ‚ü®S, hl, e‚ü© := cof_eq r
      type_eq_zero_iff_isEmpty.2 <|
        ‚ü®fun a =>
          let ‚ü®b, h, _‚ü© := hl a
          (mk_eq_zero_iff.1 (e.trans z)).elim' ‚ü®_, h‚ü©‚ü©,
    fun e => by simp [e]‚ü©

theorem cof_ne_zero {o} : cof o ‚â† 0 ‚Üî o ‚â† 0 :=
  cof_eq_zero.not

@[simp]
theorem cof_succ (o) : cof (succ o) = 1 := by
  apply le_antisymm
  ¬∑ refine inductionOn o fun Œ± r _ => ?_
    change cof (type _) ‚â§ _
    rw [‚Üê (_ : #_ = 1)]
    ¬∑ apply cof_type_le
      refine fun a => ‚ü®Sum.inr PUnit.unit, Set.mem_singleton _, ?_‚ü©
      rcases a with (a | ‚ü®‚ü®‚ü®‚ü©‚ü©‚ü©) <;> simp [EmptyRelation]
    ¬∑ rw [Cardinal.mk_fintype, Set.card_singleton]
      simp
  ¬∑ rw [‚Üê Cardinal.succ_zero, succ_le_iff]
    simpa [lt_iff_le_and_ne, Cardinal.zero_le] using fun h =>
      succ_ne_zero o (cof_eq_zero.1 (Eq.symm h))

@[simp]
theorem cof_eq_one_iff_is_succ {o} : cof.{u} o = 1 ‚Üî ‚àÉ a, o = succ a :=
  ‚ü®inductionOn o fun Œ± r _ z => by
      rcases cof_eq r with ‚ü®S, hl, e‚ü©; rw [z] at e
      cases' mk_ne_zero_iff.1 (by rw [e]; exact one_ne_zero) with a
      refine
        ‚ü®typein r a,
          Eq.symm <|
            Quotient.sound
              ‚ü®RelIso.ofSurjective (RelEmbedding.ofMonotone ?_ fun x y => ?_) fun x => ?_‚ü©‚ü©
      ¬∑ apply Sum.rec <;> [exact Subtype.val; exact fun _ => a]
      ¬∑ rcases x with (x | ‚ü®‚ü®‚ü®‚ü©‚ü©‚ü©) <;> rcases y with (y | ‚ü®‚ü®‚ü®‚ü©‚ü©‚ü©) <;>
          simp [Subrel, Order.Preimage, EmptyRelation]
        exact x.2
      ¬∑ suffices r x a ‚à® ‚àÉ _ : PUnit.{u}, ‚Üëa = x by
          convert this
          dsimp [RelEmbedding.ofMonotone]; simp
        rcases trichotomous_of r x a with (h | h | h)
        ¬∑ exact Or.inl h
        ¬∑ exact Or.inr ‚ü®PUnit.unit, h.symm‚ü©
        ¬∑ rcases hl x with ‚ü®a', aS, hn‚ü©
          refine absurd h ?_
          convert hn
          change _ = ‚Üë(‚ü®a', aS‚ü© : S)
          have := le_one_iff_subsingleton.1 (le_of_eq e)
          congr!,
    fun ‚ü®a, e‚ü© => by simp [e]‚ü©

/-- A fundamental sequence for `a` is an increasing sequence of length `o = cof a` that converges at
    `a`. We provide `o` explicitly in order to avoid type rewrites. -/
def IsFundamentalSequence (a o : Ordinal.{u}) (f : ‚àÄ b < o, Ordinal.{u}) : Prop :=
  o ‚â§ a.cof.ord ‚àß (‚àÄ {i j} (hi hj), i < j ‚Üí f i hi < f j hj) ‚àß blsub.{u, u} o f = a

namespace IsFundamentalSequence

variable {a o : Ordinal.{u}} {f : ‚àÄ b < o, Ordinal.{u}}

protected theorem cof_eq (hf : IsFundamentalSequence a o f) : a.cof.ord = o :=
  hf.1.antisymm' <| by
    rw [‚Üê hf.2.2]
    exact (ord_le_ord.2 (cof_blsub_le f)).trans (ord_card_le o)

protected theorem strict_mono (hf : IsFundamentalSequence a o f) {i j} :
    ‚àÄ hi hj, i < j ‚Üí f i hi < f j hj :=
  hf.2.1

theorem blsub_eq (hf : IsFundamentalSequence a o f) : blsub.{u, u} o f = a :=
  hf.2.2

theorem ord_cof (hf : IsFundamentalSequence a o f) :
    IsFundamentalSequence a a.cof.ord fun i hi => f i (hi.trans_le (by rw [hf.cof_eq])) := by
  have H := hf.cof_eq
  subst H
  exact hf

theorem id_of_le_cof (h : o ‚â§ o.cof.ord) : IsFundamentalSequence o o fun a _ => a :=
  ‚ü®h, @fun _ _ _ _ => id, blsub_id o‚ü©

protected theorem zero {f : ‚àÄ b < (0 : Ordinal), Ordinal} : IsFundamentalSequence 0 0 f :=
  ‚ü®by rw [cof_zero, ord_zero], @fun i j hi => (Ordinal.not_lt_zero i hi).elim, blsub_zero f‚ü©

protected theorem succ : IsFundamentalSequence (succ o) 1 fun _ _ => o := by
  refine ‚ü®?_, @fun i j hi hj h => ?_, blsub_const Ordinal.one_ne_zero o‚ü©
  ¬∑ rw [cof_succ, ord_one]
  ¬∑ rw [lt_one_iff_zero] at hi hj
    rw [hi, hj] at h
    exact h.false.elim

protected theorem monotone (hf : IsFundamentalSequence a o f) {i j : Ordinal} (hi : i < o)
    (hj : j < o) (hij : i ‚â§ j) : f i hi ‚â§ f j hj := by
  rcases lt_or_eq_of_le hij with (hij | rfl)
  ¬∑ exact (hf.2.1 hi hj hij).le
  ¬∑ rfl

theorem trans {a o o' : Ordinal.{u}} {f : ‚àÄ b < o, Ordinal.{u}} (hf : IsFundamentalSequence a o f)
    {g : ‚àÄ b < o', Ordinal.{u}} (hg : IsFundamentalSequence o o' g) :
    IsFundamentalSequence a o' fun i hi =>
      f (g i hi) (by rw [‚Üê hg.2.2]; apply lt_blsub) := by
  refine ‚ü®?_, @fun i j _ _ h => hf.2.1 _ _ (hg.2.1 _ _ h), ?_‚ü©
  ¬∑ rw [hf.cof_eq]
    exact hg.1.trans (ord_cof_le o)
  ¬∑ rw [@blsub_comp.{u, u, u} o _ f (@IsFundamentalSequence.monotone _ _ f hf)]
    ¬∑ exact hf.2.2
    ¬∑ exact hg.2.2

end IsFundamentalSequence

/-- Every ordinal has a fundamental sequence. -/
theorem exists_fundamental_sequence (a : Ordinal.{u}) :
    ‚àÉ f, IsFundamentalSequence a a.cof.ord f := by
  suffices h : ‚àÉ o f, IsFundamentalSequence a o f by
    rcases h with ‚ü®o, f, hf‚ü©
    exact ‚ü®_, hf.ord_cof‚ü©
  rcases exists_lsub_cof a with ‚ü®Œπ, f, hf, hŒπ‚ü©
  rcases ord_eq Œπ with ‚ü®r, wo, hr‚ü©
  haveI := wo
  let r' := Subrel r { i | ‚àÄ j, r j i ‚Üí f j < f i }
  let hrr' : r' ‚Ü™r r := Subrel.relEmbedding _ _
  haveI := hrr'.isWellOrder
  refine
    ‚ü®_, _, hrr'.ordinal_type_le.trans ?_, @fun i j _ h _ => (enum r' ‚ü®j, h‚ü©).prop _ ?_,
      le_antisymm (blsub_le fun i hi => lsub_le_iff.1 hf.le _) ?_‚ü©
  ¬∑ rw [‚Üê hŒπ, hr]
  ¬∑ change r (hrr'.1 _) (hrr'.1 _)
    rwa [hrr'.2, @enum_lt_enum _ r']
  ¬∑ rw [‚Üê hf, lsub_le_iff]
    intro i
    suffices h : ‚àÉ i' hi', f i ‚â§ bfamilyOfFamily' r' (fun i => f i) i' hi' by
      rcases h with ‚ü®i', hi', hfg‚ü©
      exact hfg.trans_lt (lt_blsub _ _ _)
    by_cases h : ‚àÄ j, r j i ‚Üí f j < f i
    ¬∑ refine ‚ü®typein r' ‚ü®i, h‚ü©, typein_lt_type _ _, ?_‚ü©
      rw [bfamilyOfFamily'_typein]
    ¬∑ push_neg at h
      cases' wo.wf.min_mem _ h with hji hij
      refine ‚ü®typein r' ‚ü®_, fun k hkj => lt_of_lt_of_le ?_ hij‚ü©, typein_lt_type _ _, ?_‚ü©
      ¬∑ by_contra! H
        exact (wo.wf.not_lt_min _ h ‚ü®IsTrans.trans _ _ _ hkj hji, H‚ü©) hkj
      ¬∑ rwa [bfamilyOfFamily'_typein]

@[simp]
theorem cof_cof (a : Ordinal.{u}) : cof (cof a).ord = cof a := by
  cases' exists_fundamental_sequence a with f hf
  cases' exists_fundamental_sequence a.cof.ord with g hg
  exact ord_injective (hf.trans hg).cof_eq.symm

protected theorem IsNormal.isFundamentalSequence {f : Ordinal.{u} ‚Üí Ordinal.{u}} (hf : IsNormal f)
    {a o} (ha : IsLimit a) {g} (hg : IsFundamentalSequence a o g) :
    IsFundamentalSequence (f a) o fun b hb => f (g b hb) := by
  refine ‚ü®?_, @fun i j _ _ h => hf.strictMono (hg.2.1 _ _ h), ?_‚ü©
  ¬∑ rcases exists_lsub_cof (f a) with ‚ü®Œπ, f', hf', hŒπ‚ü©
    rw [‚Üê hg.cof_eq, ord_le_ord, ‚Üê hŒπ]
    suffices (lsub.{u, u} fun i => sInf { b : Ordinal | f' i ‚â§ f b }) = a by
      rw [‚Üê this]
      apply cof_lsub_le
    have H : ‚àÄ i, ‚àÉ b < a, f' i ‚â§ f b := fun i => by
      have := lt_lsub.{u, u} f' i
      rw [hf', ‚Üê IsNormal.blsub_eq.{u, u} hf ha, lt_blsub_iff] at this
      simpa using this
    refine (lsub_le fun i => ?_).antisymm (le_of_forall_lt fun b hb => ?_)
    ¬∑ rcases H i with ‚ü®b, hb, hb'‚ü©
      exact lt_of_le_of_lt (csInf_le' hb') hb
    ¬∑ have := hf.strictMono hb
      rw [‚Üê hf', lt_lsub_iff] at this
      cases' this with i hi
      rcases H i with ‚ü®b, _, hb‚ü©
      exact
        ((le_csInf_iff'' ‚ü®b, by exact hb‚ü©).2 fun c hc =>
          hf.strictMono.le_iff_le.1 (hi.trans hc)).trans_lt (lt_lsub _ i)
  ¬∑ rw [@blsub_comp.{u, u, u} a _ (fun b _ => f b) (@fun i j _ _ h => hf.strictMono.monotone h) g
        hg.2.2]
    exact IsNormal.blsub_eq.{u, u} hf ha

theorem IsNormal.cof_eq {f} (hf : IsNormal f) {a} (ha : IsLimit a) : cof (f a) = cof a :=
  let ‚ü®_, hg‚ü© := exists_fundamental_sequence a
  ord_injective (hf.isFundamentalSequence ha hg).cof_eq

theorem IsNormal.cof_le {f} (hf : IsNormal f) (a) : cof a ‚â§ cof (f a) := by
  rcases zero_or_succ_or_limit a with (rfl | ‚ü®b, rfl‚ü© | ha)
  ¬∑ rw [cof_zero]
    exact zero_le _
  ¬∑ rw [cof_succ, Cardinal.one_le_iff_ne_zero, cof_ne_zero, ‚Üê Ordinal.pos_iff_ne_zero]
    exact (Ordinal.zero_le (f b)).trans_lt (hf.1 b)
  ¬∑ rw [hf.cof_eq ha]

@[simp]
theorem cof_add (a b : Ordinal) : b ‚â† 0 ‚Üí cof (a + b) = cof b := fun h => by
  rcases zero_or_succ_or_limit b with (rfl | ‚ü®c, rfl‚ü© | hb)
  ¬∑ contradiction
  ¬∑ rw [add_succ, cof_succ, cof_succ]
  ¬∑ exact (add_isNormal a).cof_eq hb

theorem aleph0_le_cof {o} : ‚Ñµ‚ÇÄ ‚â§ cof o ‚Üî IsLimit o := by
  rcases zero_or_succ_or_limit o with (rfl | ‚ü®o, rfl‚ü© | l)
  ¬∑ simp [not_zero_isLimit, Cardinal.aleph0_ne_zero]
  ¬∑ simp [not_succ_isLimit, Cardinal.one_lt_aleph0]
  ¬∑ simp only [l, iff_true]
    refine le_of_not_lt fun h => ?_
    cases' Cardinal.lt_aleph0.1 h with n e
    have := cof_cof o
    rw [e, ord_nat] at this
    cases n
    ¬∑ simp at e
      simp [e, not_zero_isLimit] at l
    ¬∑ rw [natCast_succ, cof_succ] at this
      rw [‚Üê this, cof_eq_one_iff_is_succ] at e
      rcases e with ‚ü®a, rfl‚ü©
      exact not_succ_isLimit _ l

@[simp]
theorem aleph'_cof {o : Ordinal} (ho : o.IsLimit) : (aleph' o).ord.cof = o.cof :=
  aleph'_isNormal.cof_eq ho

@[simp]
theorem aleph_cof {o : Ordinal} (ho : o.IsLimit) : (aleph o).ord.cof = o.cof :=
  aleph_isNormal.cof_eq ho

@[simp]
theorem cof_omega : cof œâ = ‚Ñµ‚ÇÄ :=
  (aleph0_le_cof.2 omega_isLimit).antisymm' <| by
    rw [‚Üê card_omega]
    apply cof_le_card

theorem cof_eq' (r : Œ± ‚Üí Œ± ‚Üí Prop) [IsWellOrder Œ± r] (h : IsLimit (type r)) :
    ‚àÉ S : Set Œ±, (‚àÄ a, ‚àÉ b ‚àà S, r a b) ‚àß #S = cof (type r) :=
  let ‚ü®S, H, e‚ü© := cof_eq r
  ‚ü®S, fun a =>
    let a' := enum r ‚ü®_, h.2 _ (typein_lt_type r a)‚ü©
    let ‚ü®b, h, ab‚ü© := H a'
    ‚ü®b, h,
      (IsOrderConnected.conn a b a' <|
            (typein_lt_typein r).1
              (by
                rw [typein_enum]
                exact lt_succ (typein _ _))).resolve_right
        ab‚ü©,
    e‚ü©

@[simp]
theorem cof_univ : cof univ.{u, v} = Cardinal.univ.{u, v} :=
  le_antisymm (cof_le_card _)
    (by
      refine le_of_forall_lt fun c h => ?_
      rcases lt_univ'.1 h with ‚ü®c, rfl‚ü©
      rcases @cof_eq Ordinal.{u} (¬∑ < ¬∑) _ with ‚ü®S, H, Se‚ü©
      rw [univ, ‚Üê lift_cof, ‚Üê Cardinal.lift_lift.{u+1, v, u}, Cardinal.lift_lt, ‚Üê Se]
      refine lt_of_not_ge fun h => ?_
      cases' Cardinal.lift_down h with a e
      refine Quotient.inductionOn a (fun Œ± e => ?_) e
      cases' Quotient.exact e with f
      have f := Equiv.ulift.symm.trans f
      let g a := (f a).1
      let o := succ (sup.{u, u} g)
      rcases H o with ‚ü®b, h, l‚ü©
      refine l (lt_succ_iff.2 ?_)
      rw [‚Üê show g (f.symm ‚ü®b, h‚ü©) = b by simp [g]]
      apply le_sup)

/-! ### Infinite pigeonhole principle -/


/-- If the union of s is unbounded and s is smaller than the cofinality,
  then s has an unbounded member -/
theorem unbounded_of_unbounded_sUnion (r : Œ± ‚Üí Œ± ‚Üí Prop) [wo : IsWellOrder Œ± r] {s : Set (Set Œ±)}
    (h‚ÇÅ : Unbounded r <| ‚ãÉ‚ÇÄ s) (h‚ÇÇ : #s < StrictOrder.cof r) : ‚àÉ x ‚àà s, Unbounded r x := by
  by_contra! h
  simp_rw [not_unbounded_iff] at h
  let f : s ‚Üí Œ± := fun x : s => wo.wf.sup x (h x.1 x.2)
  refine h‚ÇÇ.not_le (le_trans (csInf_le' ‚ü®range f, fun x => ?_, rfl‚ü©) mk_range_le)
  rcases h‚ÇÅ x with ‚ü®y, ‚ü®c, hc, hy‚ü©, hxy‚ü©
  exact ‚ü®f ‚ü®c, hc‚ü©, mem_range_self _, fun hxz => hxy (Trans.trans (wo.wf.lt_sup _ hy) hxz)‚ü©

/-- If the union of s is unbounded and s is smaller than the cofinality,
  then s has an unbounded member -/
theorem unbounded_of_unbounded_iUnion {Œ± Œ≤ : Type u} (r : Œ± ‚Üí Œ± ‚Üí Prop) [wo : IsWellOrder Œ± r]
    (s : Œ≤ ‚Üí Set Œ±) (h‚ÇÅ : Unbounded r <| ‚ãÉ x, s x) (h‚ÇÇ : #Œ≤ < StrictOrder.cof r) :
    ‚àÉ x : Œ≤, Unbounded r (s x) := by
  rw [‚Üê sUnion_range] at h‚ÇÅ
  rcases unbounded_of_unbounded_sUnion r h‚ÇÅ (mk_range_le.trans_lt h‚ÇÇ) with ‚ü®_, ‚ü®x, rfl‚ü©, u‚ü©
  exact ‚ü®x, u‚ü©

/-- The infinite pigeonhole principle -/
theorem infinite_pigeonhole {Œ≤ Œ± : Type u} (f : Œ≤ ‚Üí Œ±) (h‚ÇÅ : ‚Ñµ‚ÇÄ ‚â§ #Œ≤) (h‚ÇÇ : #Œ± < (#Œ≤).ord.cof) :
    ‚àÉ a : Œ±, #(f ‚Åª¬π' {a}) = #Œ≤ := by
  have : ‚àÉ a, #Œ≤ ‚â§ #(f ‚Åª¬π' {a}) := by
    by_contra! h
    apply mk_univ.not_lt
    rw [‚Üê preimage_univ, ‚Üê iUnion_of_singleton, preimage_iUnion]
    exact
      mk_iUnion_le_sum_mk.trans_lt
        ((sum_le_iSup _).trans_lt <| mul_lt_of_lt h‚ÇÅ (h‚ÇÇ.trans_le <| cof_ord_le _) (iSup_lt h‚ÇÇ h))
  cases' this with x h
  refine ‚ü®x, h.antisymm' ?_‚ü©
  rw [le_mk_iff_exists_set]
  exact ‚ü®_, rfl‚ü©

/-- Pigeonhole principle for a cardinality below the cardinality of the domain -/
theorem infinite_pigeonhole_card {Œ≤ Œ± : Type u} (f : Œ≤ ‚Üí Œ±) (Œ∏ : Cardinal) (hŒ∏ : Œ∏ ‚â§ #Œ≤)
    (h‚ÇÅ : ‚Ñµ‚ÇÄ ‚â§ Œ∏) (h‚ÇÇ : #Œ± < Œ∏.ord.cof) : ‚àÉ a : Œ±, Œ∏ ‚â§ #(f ‚Åª¬π' {a}) := by
  rcases le_mk_iff_exists_set.1 hŒ∏ with ‚ü®s, rfl‚ü©
  cases' infinite_pigeonhole (f ‚àò Subtype.val : s ‚Üí Œ±) h‚ÇÅ h‚ÇÇ with a ha
  use a; rw [‚Üê ha, @preimage_comp _ _ _ Subtype.val f]
  exact mk_preimage_of_injective _ _ Subtype.val_injective

theorem infinite_pigeonhole_set {Œ≤ Œ± : Type u} {s : Set Œ≤} (f : s ‚Üí Œ±) (Œ∏ : Cardinal)
    (hŒ∏ : Œ∏ ‚â§ #s) (h‚ÇÅ : ‚Ñµ‚ÇÄ ‚â§ Œ∏) (h‚ÇÇ : #Œ± < Œ∏.ord.cof) :
    ‚àÉ (a : Œ±) (t : Set Œ≤) (h : t ‚äÜ s), Œ∏ ‚â§ #t ‚àß ‚àÄ ‚¶Éx‚¶Ñ (hx : x ‚àà t), f ‚ü®x, h hx‚ü© = a := by
  cases' infinite_pigeonhole_card f Œ∏ hŒ∏ h‚ÇÅ h‚ÇÇ with a ha
  refine ‚ü®a, { x | ‚àÉ h, f ‚ü®x, h‚ü© = a }, ?_, ?_, ?_‚ü©
  ¬∑ rintro x ‚ü®hx, _‚ü©
    exact hx
  ¬∑ refine
      ha.trans
        (ge_of_eq <|
          Quotient.sound ‚ü®Equiv.trans ?_ (Equiv.subtypeSubtypeEquivSubtypeExists _ _).symm‚ü©)
    simp only [coe_eq_subtype, mem_singleton_iff, mem_preimage, mem_setOf_eq]
    rfl
  rintro x ‚ü®_, hx'‚ü©; exact hx'

end Ordinal

/-! ### Regular and inaccessible cardinals -/


namespace Cardinal

open Ordinal

/-- A cardinal is a strong limit if it is not zero and it is
  closed under powersets. Note that `‚Ñµ‚ÇÄ` is a strong limit by this definition. -/
def IsStrongLimit (c : Cardinal) : Prop :=
  c ‚â† 0 ‚àß ‚àÄ x < c, (2^x) < c

theorem IsStrongLimit.ne_zero {c} (h : IsStrongLimit c) : c ‚â† 0 :=
  h.1

theorem IsStrongLimit.two_power_lt {x c} (h : IsStrongLimit c) : x < c ‚Üí (2^x) < c :=
  h.2 x

theorem isStrongLimit_aleph0 : IsStrongLimit ‚Ñµ‚ÇÄ :=
  ‚ü®aleph0_ne_zero, fun x hx => by
    rcases lt_aleph0.1 hx with ‚ü®n, rfl‚ü©
    exact mod_cast nat_lt_aleph0 (2 ^ n)‚ü©

protected theorem IsStrongLimit.isSuccPrelimit {c} (H : IsStrongLimit c) : IsSuccPrelimit c :=
  isSuccPrelimit_of_succ_lt fun x h => (succ_le_of_lt <| cantor x).trans_lt (H.two_power_lt h)

@[deprecated IsStrongLimit.isSuccPrelimit (since := "2024-09-05")]
alias IsStrongLimit.isSuccLimit := IsStrongLimit.isSuccPrelimit

theorem IsStrongLimit.isLimit {c} (H : IsStrongLimit c) : IsLimit c :=
  ‚ü®H.ne_zero, H.isSuccPrelimit‚ü©

theorem isStrongLimit_beth {o : Ordinal} (H : IsSuccPrelimit o) : IsStrongLimit (beth o) := by
  rcases eq_or_ne o 0 with (rfl | h)
  ¬∑ rw [beth_zero]
    exact isStrongLimit_aleph0
  ¬∑ refine ‚ü®beth_ne_zero o, fun a ha => ?_‚ü©
    rw [beth_limit ‚ü®h, isSuccPrelimit_iff_succ_lt.1 H‚ü©] at ha
    rcases exists_lt_of_lt_ciSup' ha with ‚ü®‚ü®i, hi‚ü©, ha‚ü©
    have := power_le_power_left two_ne_zero ha.le
    rw [‚Üê beth_succ] at this
    exact this.trans_lt (beth_lt.2 (H.succ_lt hi))

theorem mk_bounded_subset {Œ± : Type*} (h : ‚àÄ x < #Œ±, (2^x) < #Œ±) {r : Œ± ‚Üí Œ± ‚Üí Prop}
    [IsWellOrder Œ± r] (hr : (#Œ±).ord = type r) : #{ s : Set Œ± // Bounded r s } = #Œ± := by
  rcases eq_or_ne #Œ± 0 with (ha | ha)
  ¬∑ rw [ha]
    haveI := mk_eq_zero_iff.1 ha
    rw [mk_eq_zero_iff]
    constructor
    rintro ‚ü®s, hs‚ü©
    exact (not_unbounded_iff s).2 hs (unbounded_of_isEmpty s)
  have h' : IsStrongLimit #Œ± := ‚ü®ha, h‚ü©
  have ha := h'.isLimit.aleph0_le
  apply le_antisymm
  ¬∑ have : { s : Set Œ± | Bounded r s } = ‚ãÉ i, ùí´{ j | r j i } := setOf_exists _
    rw [‚Üê coe_setOf, this]
    refine mk_iUnion_le_sum_mk.trans ((sum_le_iSup (fun i => #(ùí´{ j | r j i }))).trans
      ((mul_le_max_of_aleph0_le_left ha).trans ?_))
    rw [max_eq_left]
    apply ciSup_le' _
    intro i
    rw [mk_powerset]
    apply (h'.two_power_lt _).le
    rw [coe_setOf, card_typein, ‚Üê lt_ord, hr]
    apply typein_lt_type
  ¬∑ refine @mk_le_of_injective Œ± _ (fun x => Subtype.mk {x} ?_) ?_
    ¬∑ apply bounded_singleton
      rw [‚Üê hr]
      apply ord_isLimit ha
    ¬∑ intro a b hab
      simpa [singleton_eq_singleton_iff] using hab

theorem mk_subset_mk_lt_cof {Œ± : Type*} (h : ‚àÄ x < #Œ±, (2^x) < #Œ±) :
    #{ s : Set Œ± // #s < cof (#Œ±).ord } = #Œ± := by
  rcases eq_or_ne #Œ± 0 with (ha | ha)
  ¬∑ simp [ha]
  have h' : IsStrongLimit #Œ± := ‚ü®ha, h‚ü©
  rcases ord_eq Œ± with ‚ü®r, wo, hr‚ü©
  haveI := wo
  apply le_antisymm
  ¬∑ conv_rhs => rw [‚Üê mk_bounded_subset h hr]
    apply mk_le_mk_of_subset
    intro s hs
    rw [hr] at hs
    exact lt_cof_type hs
  ¬∑ refine @mk_le_of_injective Œ± _ (fun x => Subtype.mk {x} ?_) ?_
    ¬∑ rw [mk_singleton]
      exact one_lt_aleph0.trans_le (aleph0_le_cof.2 (ord_isLimit h'.isLimit.aleph0_le))
    ¬∑ intro a b hab
      simpa [singleton_eq_singleton_iff] using hab

/-- A cardinal is regular if it is infinite and it equals its own cofinality. -/
def IsRegular (c : Cardinal) : Prop :=
  ‚Ñµ‚ÇÄ ‚â§ c ‚àß c ‚â§ c.ord.cof

theorem IsRegular.aleph0_le {c : Cardinal} (H : c.IsRegular) : ‚Ñµ‚ÇÄ ‚â§ c :=
  H.1

theorem IsRegular.cof_eq {c : Cardinal} (H : c.IsRegular) : c.ord.cof = c :=
  (cof_ord_le c).antisymm H.2

theorem IsRegular.pos {c : Cardinal} (H : c.IsRegular) : 0 < c :=
  aleph0_pos.trans_le H.1

theorem IsRegular.nat_lt {c : Cardinal} (H : c.IsRegular) (n : ‚Ñï) : n < c :=
  lt_of_lt_of_le (nat_lt_aleph0 n) H.aleph0_le

theorem IsRegular.ord_pos {c : Cardinal} (H : c.IsRegular) : 0 < c.ord := by
  rw [Cardinal.lt_ord, card_zero]
  exact H.pos

theorem isRegular_cof {o : Ordinal} (h : o.IsLimit) : IsRegular o.cof :=
  ‚ü®aleph0_le_cof.2 h, (cof_cof o).ge‚ü©

theorem isRegular_aleph0 : IsRegular ‚Ñµ‚ÇÄ :=
  ‚ü®le_rfl, by simp‚ü©

theorem isRegular_succ {c : Cardinal.{u}} (h : ‚Ñµ‚ÇÄ ‚â§ c) : IsRegular (succ c) :=
  ‚ü®h.trans (le_succ c),
    succ_le_of_lt
      (by
        cases' Quotient.exists_rep (@succ Cardinal _ _ c) with Œ± Œ±e; simp only [mk'_def] at Œ±e
        rcases ord_eq Œ± with ‚ü®r, wo, re‚ü©
        have := ord_isLimit (h.trans (le_succ _))
        rw [‚Üê Œ±e, re] at this ‚ä¢
        rcases cof_eq' r this with ‚ü®S, H, Se‚ü©
        rw [‚Üê Se]
        apply lt_imp_lt_of_le_imp_le fun h => mul_le_mul_right' h c
        rw [mul_eq_self h, ‚Üê succ_le_iff, ‚Üê Œ±e, ‚Üê sum_const']
        refine le_trans ?_ (sum_le_sum (fun (x : S) => card (typein r (x : Œ±))) _ fun i => ?_)
        ¬∑ simp only [‚Üê card_typein, ‚Üê mk_sigma]
          exact
            ‚ü®Embedding.ofSurjective (fun x => x.2.1) fun a =>
                let ‚ü®b, h, ab‚ü© := H a
                ‚ü®‚ü®‚ü®_, h‚ü©, _, ab‚ü©, rfl‚ü©‚ü©
        ¬∑ rw [‚Üê lt_succ_iff, ‚Üê lt_ord, ‚Üê Œ±e, re]
          apply typein_lt_type)‚ü©

theorem isRegular_aleph_one : IsRegular (aleph 1) := by
  rw [‚Üê succ_aleph0]
  exact isRegular_succ le_rfl

theorem isRegular_aleph'_succ {o : Ordinal} (h : œâ ‚â§ o) : IsRegular (aleph' (succ o)) := by
  rw [aleph'_succ]
  exact isRegular_succ (aleph0_le_aleph'.2 h)

theorem isRegular_aleph_succ (o : Ordinal) : IsRegular (aleph (succ o)) := by
  rw [aleph_succ]
  exact isRegular_succ (aleph0_le_aleph o)

/-- A function whose codomain's cardinality is infinite but strictly smaller than its domain's
has a fiber with cardinality strictly great than the codomain.
-/
theorem infinite_pigeonhole_card_lt {Œ≤ Œ± : Type u} (f : Œ≤ ‚Üí Œ±) (w : #Œ± < #Œ≤) (w' : ‚Ñµ‚ÇÄ ‚â§ #Œ±) :
    ‚àÉ a : Œ±, #Œ± < #(f ‚Åª¬π' {a}) := by
  simp_rw [‚Üê succ_le_iff]
  exact
    Ordinal.infinite_pigeonhole_card f (succ #Œ±) (succ_le_of_lt w) (w'.trans (lt_succ _).le)
      ((lt_succ _).trans_le (isRegular_succ w').2.ge)

/-- A function whose codomain's cardinality is infinite but strictly smaller than its domain's
has an infinite fiber.
-/
theorem exists_infinite_fiber {Œ≤ Œ± : Type u} (f : Œ≤ ‚Üí Œ±) (w : #Œ± < #Œ≤) (w' : Infinite Œ±) :
    ‚àÉ a : Œ±, Infinite (f ‚Åª¬π' {a}) := by
  simp_rw [Cardinal.infinite_iff] at w' ‚ä¢
  cases' infinite_pigeonhole_card_lt f w w' with a ha
  exact ‚ü®a, w'.trans ha.le‚ü©

/-- If an infinite type `Œ≤` can be expressed as a union of finite sets,
then the cardinality of the collection of those finite sets
must be at least the cardinality of `Œ≤`.
-/
theorem le_range_of_union_finset_eq_top {Œ± Œ≤ : Type*} [Infinite Œ≤] (f : Œ± ‚Üí Finset Œ≤)
    (w : ‚ãÉ a, (f a : Set Œ≤) = ‚ä§) : #Œ≤ ‚â§ #(range f) := by
  have k : _root_.Infinite (range f) := by
    rw [infinite_coe_iff]
    apply mt (union_finset_finite_of_range_finite f)
    rw [w]
    exact infinite_univ
  by_contra h
  simp only [not_le] at h
  let u : ‚àÄ b, ‚àÉ a, b ‚àà f a := fun b => by simpa using (w.ge : _) (Set.mem_univ b)
  let u' : Œ≤ ‚Üí range f := fun b => ‚ü®f (u b).choose, by simp‚ü©
  have v' : ‚àÄ a, u' ‚Åª¬π' {‚ü®f a, by simp‚ü©} ‚â§ f a := by
    rintro a p m
    simp? [u']  at m says simp only [mem_preimage, mem_singleton_iff, Subtype.mk.injEq, u'] at m
    rw [‚Üê m]
    apply fun b => (u b).choose_spec
  obtain ‚ü®‚ü®-, ‚ü®a, rfl‚ü©‚ü©, p‚ü© := exists_infinite_fiber u' h k
  exact (@Infinite.of_injective _ _ p (inclusion (v' a)) (inclusion_injective _)).false

theorem lsub_lt_ord_lift_of_isRegular {Œπ} {f : Œπ ‚Üí Ordinal} {c} (hc : IsRegular c)
    (hŒπ : Cardinal.lift.{v, u} #Œπ < c) : (‚àÄ i, f i < c.ord) ‚Üí Ordinal.lsub.{u, v} f < c.ord :=
  lsub_lt_ord_lift (by rwa [hc.cof_eq])

theorem lsub_lt_ord_of_isRegular {Œπ} {f : Œπ ‚Üí Ordinal} {c} (hc : IsRegular c) (hŒπ : #Œπ < c) :
    (‚àÄ i, f i < c.ord) ‚Üí Ordinal.lsub f < c.ord :=
  lsub_lt_ord (by rwa [hc.cof_eq])

theorem sup_lt_ord_lift_of_isRegular {Œπ} {f : Œπ ‚Üí Ordinal} {c} (hc : IsRegular c)
    (hŒπ : Cardinal.lift.{v, u} #Œπ < c) : (‚àÄ i, f i < c.ord) ‚Üí Ordinal.sup.{u, v} f < c.ord :=
  sup_lt_ord_lift (by rwa [hc.cof_eq])

theorem sup_lt_ord_of_isRegular {Œπ} {f : Œπ ‚Üí Ordinal} {c} (hc : IsRegular c) (hŒπ : #Œπ < c) :
    (‚àÄ i, f i < c.ord) ‚Üí Ordinal.sup f < c.ord :=
  sup_lt_ord (by rwa [hc.cof_eq])

theorem blsub_lt_ord_lift_of_isRegular {o : Ordinal} {f : ‚àÄ a < o, Ordinal} {c} (hc : IsRegular c)
    (ho : Cardinal.lift.{v, u} o.card < c) :
    (‚àÄ i hi, f i hi < c.ord) ‚Üí Ordinal.blsub.{u, v} o f < c.ord :=
  blsub_lt_ord_lift (by rwa [hc.cof_eq])

theorem blsub_lt_ord_of_isRegular {o : Ordinal} {f : ‚àÄ a < o, Ordinal} {c} (hc : IsRegular c)
    (ho : o.card < c) : (‚àÄ i hi, f i hi < c.ord) ‚Üí Ordinal.blsub o f < c.ord :=
  blsub_lt_ord (by rwa [hc.cof_eq])

theorem bsup_lt_ord_lift_of_isRegular {o : Ordinal} {f : ‚àÄ a < o, Ordinal} {c} (hc : IsRegular c)
    (hŒπ : Cardinal.lift.{v, u} o.card < c) :
    (‚àÄ i hi, f i hi < c.ord) ‚Üí Ordinal.bsup.{u, v} o f < c.ord :=
  bsup_lt_ord_lift (by rwa [hc.cof_eq])

theorem bsup_lt_ord_of_isRegular {o : Ordinal} {f : ‚àÄ a < o, Ordinal} {c} (hc : IsRegular c)
    (hŒπ : o.card < c) : (‚àÄ i hi, f i hi < c.ord) ‚Üí Ordinal.bsup o f < c.ord :=
  bsup_lt_ord (by rwa [hc.cof_eq])

theorem iSup_lt_lift_of_isRegular {Œπ} {f : Œπ ‚Üí Cardinal} {c} (hc : IsRegular c)
    (hŒπ : Cardinal.lift.{v, u} #Œπ < c) : (‚àÄ i, f i < c) ‚Üí iSup.{max u v + 1, u + 1} f < c :=
  iSup_lt_lift.{u, v} (by rwa [hc.cof_eq])

theorem iSup_lt_of_isRegular {Œπ} {f : Œπ ‚Üí Cardinal} {c} (hc : IsRegular c) (hŒπ : #Œπ < c) :
    (‚àÄ i, f i < c) ‚Üí iSup f < c :=
  iSup_lt (by rwa [hc.cof_eq])

theorem sum_lt_lift_of_isRegular {Œπ : Type u} {f : Œπ ‚Üí Cardinal} {c : Cardinal} (hc : IsRegular c)
    (hŒπ : Cardinal.lift.{v, u} #Œπ < c) (hf : ‚àÄ i, f i < c) : sum f < c :=
  (sum_le_iSup_lift _).trans_lt <| mul_lt_of_lt hc.1 hŒπ (iSup_lt_lift_of_isRegular hc hŒπ hf)

theorem sum_lt_of_isRegular {Œπ : Type u} {f : Œπ ‚Üí Cardinal} {c : Cardinal} (hc : IsRegular c)
    (hŒπ : #Œπ < c) : (‚àÄ i, f i < c) ‚Üí sum f < c :=
  sum_lt_lift_of_isRegular.{u, u} hc (by rwa [lift_id])

@[simp]
theorem card_lt_of_card_iUnion_lt {Œπ : Type u} {Œ± : Type u} {t : Œπ ‚Üí Set Œ±} {c : Cardinal}
    (h : #(‚ãÉ i, t i) < c) (i : Œπ) : #(t i) < c :=
  lt_of_le_of_lt (Cardinal.mk_le_mk_of_subset <| subset_iUnion _ _) h

@[simp]
theorem card_iUnion_lt_iff_forall_of_isRegular {Œπ : Type u} {Œ± : Type u} {t : Œπ ‚Üí Set Œ±}
    {c : Cardinal} (hc : c.IsRegular) (hŒπ : #Œπ < c) : #(‚ãÉ i, t i) < c ‚Üî ‚àÄ i, #(t i) < c := by
  refine ‚ü®card_lt_of_card_iUnion_lt, fun h ‚Ü¶ ?_‚ü©
  apply lt_of_le_of_lt (Cardinal.mk_sUnion_le _)
  apply Cardinal.mul_lt_of_lt hc.aleph0_le
    (lt_of_le_of_lt Cardinal.mk_range_le hŒπ)
  apply Cardinal.iSup_lt_of_isRegular hc (lt_of_le_of_lt Cardinal.mk_range_le hŒπ)
  simpa

theorem card_lt_of_card_biUnion_lt {Œ± Œ≤ : Type u} {s : Set Œ±} {t : ‚àÄ a ‚àà s, Set Œ≤} {c : Cardinal}
    (h : #(‚ãÉ a ‚àà s, t a ‚Äπ_‚Ä∫) < c) (a : Œ±) (ha : a ‚àà s) : # (t a ha) < c := by
  rw [biUnion_eq_iUnion] at h
  have := card_lt_of_card_iUnion_lt h
  simp_all only [iUnion_coe_set,
    Subtype.forall]

theorem card_biUnion_lt_iff_forall_of_isRegular {Œ± Œ≤ : Type u} {s : Set Œ±} {t : ‚àÄ a ‚àà s, Set Œ≤}
    {c : Cardinal} (hc : c.IsRegular) (hs : #s < c) :
    #(‚ãÉ a ‚àà s, t a ‚Äπ_‚Ä∫) < c ‚Üî ‚àÄ a (ha : a ‚àà s), # (t a ha) < c := by
  rw [biUnion_eq_iUnion, card_iUnion_lt_iff_forall_of_isRegular hc hs, SetCoe.forall']

theorem nfpFamily_lt_ord_lift_of_isRegular {Œπ} {f : Œπ ‚Üí Ordinal ‚Üí Ordinal} {c} (hc : IsRegular c)
    (hŒπ : Cardinal.lift.{v, u} #Œπ < c) (hc' : c ‚â† ‚Ñµ‚ÇÄ) (hf : ‚àÄ (i), ‚àÄ b < c.ord, f i b < c.ord) {a}
    (ha : a < c.ord) : nfpFamily.{u, v} f a < c.ord := by
  apply nfpFamily_lt_ord_lift.{u, v} _ _ hf ha <;> rw [hc.cof_eq]
  ¬∑ exact lt_of_le_of_ne hc.1 hc'.symm
  ¬∑ exact hŒπ

theorem nfpFamily_lt_ord_of_isRegular {Œπ} {f : Œπ ‚Üí Ordinal ‚Üí Ordinal} {c} (hc : IsRegular c)
    (hŒπ : #Œπ < c) (hc' : c ‚â† ‚Ñµ‚ÇÄ) {a} (hf : ‚àÄ (i), ‚àÄ b < c.ord, f i b < c.ord) :
    a < c.ord ‚Üí nfpFamily.{u, u} f a < c.ord :=
  nfpFamily_lt_ord_lift_of_isRegular hc (by rwa [lift_id]) hc' hf

theorem nfpBFamily_lt_ord_lift_of_isRegular {o : Ordinal} {f : ‚àÄ a < o, Ordinal ‚Üí Ordinal} {c}
    (hc : IsRegular c) (ho : Cardinal.lift.{v, u} o.card < c) (hc' : c ‚â† ‚Ñµ‚ÇÄ)
    (hf : ‚àÄ (i hi), ‚àÄ b < c.ord, f i hi b < c.ord) {a} :
    a < c.ord ‚Üí nfpBFamily.{u, v} o f a < c.ord :=
  nfpFamily_lt_ord_lift_of_isRegular hc (by rwa [mk_toType]) hc' fun i => hf _ _

theorem nfpBFamily_lt_ord_of_isRegular {o : Ordinal} {f : ‚àÄ a < o, Ordinal ‚Üí Ordinal} {c}
    (hc : IsRegular c) (ho : o.card < c) (hc' : c ‚â† ‚Ñµ‚ÇÄ)
    (hf : ‚àÄ (i hi), ‚àÄ b < c.ord, f i hi b < c.ord) {a} :
    a < c.ord ‚Üí nfpBFamily.{u, u} o f a < c.ord :=
  nfpBFamily_lt_ord_lift_of_isRegular hc (by rwa [lift_id]) hc' hf

theorem nfp_lt_ord_of_isRegular {f : Ordinal ‚Üí Ordinal} {c} (hc : IsRegular c) (hc' : c ‚â† ‚Ñµ‚ÇÄ)
    (hf : ‚àÄ i < c.ord, f i < c.ord) {a} : a < c.ord ‚Üí nfp f a < c.ord :=
  nfp_lt_ord
    (by
      rw [hc.cof_eq]
      exact lt_of_le_of_ne hc.1 hc'.symm)
    hf

theorem derivFamily_lt_ord_lift {Œπ} {f : Œπ ‚Üí Ordinal ‚Üí Ordinal} {c} (hc : IsRegular c)
    (hŒπ : Cardinal.lift.{v, u} #Œπ < c) (hc' : c ‚â† ‚Ñµ‚ÇÄ)
    (hf : ‚àÄ (i), ‚àÄ b < c.ord, f i b < c.ord) {a} :
    a < c.ord ‚Üí derivFamily.{u, v} f a < c.ord := by
  have hœâ : ‚Ñµ‚ÇÄ < c.ord.cof := by
    rw [hc.cof_eq]
    exact lt_of_le_of_ne hc.1 hc'.symm
  induction a using limitRecOn with
  | H‚ÇÅ =>
    rw [derivFamily_zero]
    exact nfpFamily_lt_ord_lift hœâ (by rwa [hc.cof_eq]) hf
  | H‚ÇÇ b hb =>
    intro hb'
    rw [derivFamily_succ]
    exact
      nfpFamily_lt_ord_lift hœâ (by rwa [hc.cof_eq]) hf
        ((ord_isLimit hc.1).2 _ (hb ((lt_succ b).trans hb')))
  | H‚ÇÉ b hb H =>
    intro hb'
    rw [derivFamily_limit f hb]
    exact
      bsup_lt_ord_of_isRegular.{u, v} hc (ord_lt_ord.1 ((ord_card_le b).trans_lt hb')) fun o' ho' =>
        H o' ho' (ho'.trans hb')

theorem derivFamily_lt_ord {Œπ} {f : Œπ ‚Üí Ordinal ‚Üí Ordinal} {c} (hc : IsRegular c) (hŒπ : #Œπ < c)
    (hc' : c ‚â† ‚Ñµ‚ÇÄ) (hf : ‚àÄ (i), ‚àÄ b < c.ord, f i b < c.ord) {a} :
    a < c.ord ‚Üí derivFamily.{u, u} f a < c.ord :=
  derivFamily_lt_ord_lift hc (by rwa [lift_id]) hc' hf

theorem derivBFamily_lt_ord_lift {o : Ordinal} {f : ‚àÄ a < o, Ordinal ‚Üí Ordinal} {c}
    (hc : IsRegular c) (hŒπ : Cardinal.lift.{v, u} o.card < c) (hc' : c ‚â† ‚Ñµ‚ÇÄ)
    (hf : ‚àÄ (i hi), ‚àÄ b < c.ord, f i hi b < c.ord) {a} :
    a < c.ord ‚Üí derivBFamily.{u, v} o f a < c.ord :=
  derivFamily_lt_ord_lift hc (by rwa [mk_toType]) hc' fun i => hf _ _

theorem derivBFamily_lt_ord {o : Ordinal} {f : ‚àÄ a < o, Ordinal ‚Üí Ordinal} {c} (hc : IsRegular c)
    (hŒπ : o.card < c) (hc' : c ‚â† ‚Ñµ‚ÇÄ) (hf : ‚àÄ (i hi), ‚àÄ b < c.ord, f i hi b < c.ord) {a} :
    a < c.ord ‚Üí derivBFamily.{u, u} o f a < c.ord :=
  derivBFamily_lt_ord_lift hc (by rwa [lift_id]) hc' hf

theorem deriv_lt_ord {f : Ordinal.{u} ‚Üí Ordinal} {c} (hc : IsRegular c) (hc' : c ‚â† ‚Ñµ‚ÇÄ)
    (hf : ‚àÄ i < c.ord, f i < c.ord) {a} : a < c.ord ‚Üí deriv f a < c.ord :=
  derivFamily_lt_ord_lift hc
    (by simpa using Cardinal.one_lt_aleph0.trans (lt_of_le_of_ne hc.1 hc'.symm)) hc' fun _ => hf

/-- A cardinal is inaccessible if it is an uncountable regular strong limit cardinal. -/
def IsInaccessible (c : Cardinal) :=
  ‚Ñµ‚ÇÄ < c ‚àß IsRegular c ‚àß IsStrongLimit c

theorem IsInaccessible.mk {c} (h‚ÇÅ : ‚Ñµ‚ÇÄ < c) (h‚ÇÇ : c ‚â§ c.ord.cof) (h‚ÇÉ : ‚àÄ x < c, (2^x) < c) :
    IsInaccessible c :=
  ‚ü®h‚ÇÅ, ‚ü®h‚ÇÅ.le, h‚ÇÇ‚ü©, (aleph0_pos.trans h‚ÇÅ).ne', h‚ÇÉ‚ü©

-- Lean's foundations prove the existence of ‚Ñµ‚ÇÄ many inaccessible cardinals
theorem univ_inaccessible : IsInaccessible univ.{u, v} :=
  IsInaccessible.mk (by simpa using lift_lt_univ' ‚Ñµ‚ÇÄ) (by simp) fun c h => by
    rcases lt_univ'.1 h with ‚ü®c, rfl‚ü©
    rw [‚Üê lift_two_power]
    apply lift_lt_univ'

theorem lt_power_cof {c : Cardinal.{u}} : ‚Ñµ‚ÇÄ ‚â§ c ‚Üí c < (c^cof c.ord) :=
  Quotient.inductionOn c fun Œ± h => by
    rcases ord_eq Œ± with ‚ü®r, wo, re‚ü©
    have := ord_isLimit h
    rw [mk'_def, re] at this ‚ä¢
    rcases cof_eq' r this with ‚ü®S, H, Se‚ü©
    have := sum_lt_prod (fun a : S => #{ x // r x a }) (fun _ => #Œ±) fun i => ?_
    ¬∑ simp only [Cardinal.prod_const, Cardinal.lift_id, ‚Üê Se, ‚Üê mk_sigma, power_def] at this ‚ä¢
      refine lt_of_le_of_lt ?_ this
      refine ‚ü®Embedding.ofSurjective ?_ ?_‚ü©
      ¬∑ exact fun x => x.2.1
      ¬∑ exact fun a =>
          let ‚ü®b, h, ab‚ü© := H a
          ‚ü®‚ü®‚ü®_, h‚ü©, _, ab‚ü©, rfl‚ü©
    ¬∑ have := typein_lt_type r i
      rwa [‚Üê re, lt_ord] at this

theorem lt_cof_power {a b : Cardinal} (ha : ‚Ñµ‚ÇÄ ‚â§ a) (b1 : 1 < b) : a < cof (b^a).ord := by
  have b0 : b ‚â† 0 := (zero_lt_one.trans b1).ne'
  apply lt_imp_lt_of_le_imp_le (power_le_power_left <| power_ne_zero a b0)
  rw [‚Üê power_mul, mul_eq_self ha]
  exact lt_power_cof (ha.trans <| (cantor' _ b1).le)

end Cardinal

section Omega1

namespace Ordinal

open Cardinal
open scoped Ordinal

lemma sup_sequence_lt_omega1 {Œ±} [Countable Œ±] (o : Œ± ‚Üí Ordinal) (ho : ‚àÄ n, o n < œâ‚ÇÅ) :
    sup o < œâ‚ÇÅ := by
  apply sup_lt_ord_lift _ ho
  rw [Cardinal.isRegular_aleph_one.cof_eq]
  exact lt_of_le_of_lt mk_le_aleph0 aleph0_lt_aleph_one

end Ordinal

end Omega1
