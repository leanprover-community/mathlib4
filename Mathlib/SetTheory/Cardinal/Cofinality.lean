/-
Copyright (c) 2017 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Floris van Doorn, Violeta Hern√°ndez Palacios
-/
import Mathlib.SetTheory.Cardinal.Arithmetic
import Mathlib.SetTheory.Ordinal.FixedPoint

/-!
# Cofinality

This file contains the definition of cofinality of an order and an ordinal number.

## Main Definitions

* `Order.cof r` is the cofinality of a reflexive order. This is the smallest cardinality of a subset
  `s` that is *cofinal*, i.e. `‚àÄ x, ‚àÉ y ‚àà s, r x y`.
* `Ordinal.cof o` is the cofinality of the ordinal `o` when viewed as a linear order.

## Main Statements

* `Cardinal.lt_power_cof`: A consequence of K√∂nig's theorem stating that `c < c ^ c.ord.cof` for
  `c ‚â• ‚Ñµ‚ÇÄ`.

## Implementation Notes

* The cofinality is defined for ordinals.
  If `c` is a cardinal number, its cofinality is `c.ord.cof`.
-/

noncomputable section

open Function Cardinal Set Order
open scoped Ordinal

universe u v w

variable {Œ± : Type u} {Œ≤ : Type v} {r : Œ± ‚Üí Œ± ‚Üí Prop} {s : Œ≤ ‚Üí Œ≤ ‚Üí Prop}

/-! ### Cofinality of orders -/

attribute [local instance] IsRefl.swap

namespace Order

/-- Cofinality of a reflexive order `‚âº`. This is the smallest cardinality
  of a subset `S : Set Œ±` such that `‚àÄ a, ‚àÉ b ‚àà S, a ‚âº b`. -/
def cof (r : Œ± ‚Üí Œ± ‚Üí Prop) : Cardinal :=
  sInf { c | ‚àÉ S : Set Œ±, (‚àÄ a, ‚àÉ b ‚àà S, r a b) ‚àß #S = c }

/-- The set in the definition of `Order.cof` is nonempty. -/
private theorem cof_nonempty (r : Œ± ‚Üí Œ± ‚Üí Prop) [IsRefl Œ± r] :
    { c | ‚àÉ S : Set Œ±, (‚àÄ a, ‚àÉ b ‚àà S, r a b) ‚àß #S = c }.Nonempty :=
  ‚ü®_, Set.univ, fun a => ‚ü®a, ‚ü®‚ü©, refl _‚ü©, rfl‚ü©

theorem cof_le (r : Œ± ‚Üí Œ± ‚Üí Prop) {S : Set Œ±} (h : ‚àÄ a, ‚àÉ b ‚àà S, r a b) : cof r ‚â§ #S :=
  csInf_le' ‚ü®S, h, rfl‚ü©

theorem le_cof [IsRefl Œ± r] (c : Cardinal) :
    c ‚â§ cof r ‚Üî ‚àÄ {S : Set Œ±}, (‚àÄ a, ‚àÉ b ‚àà S, r a b) ‚Üí c ‚â§ #S := by
  rw [cof, le_csInf_iff'' (cof_nonempty r)]
  use fun H S h => H _ ‚ü®S, h, rfl‚ü©
  rintro H d ‚ü®S, h, rfl‚ü©
  exact H h

end Order

namespace RelIso

private theorem cof_le_lift [IsRefl Œ≤ s] (f : r ‚âÉr s) :
    Cardinal.lift.{v} (Order.cof r) ‚â§ Cardinal.lift.{u} (Order.cof s) := by
  rw [Order.cof, Order.cof, lift_sInf, lift_sInf, le_csInf_iff'' ((Order.cof_nonempty s).image _)]
  rintro - ‚ü®-, ‚ü®u, H, rfl‚ü©, rfl‚ü©
  apply csInf_le'
  refine ‚ü®_, ‚ü®f.symm '' u, fun a => ?_, rfl‚ü©, lift_mk_eq'.2 ‚ü®(f.symm.toEquiv.image u).symm‚ü©‚ü©
  rcases H (f a) with ‚ü®b, hb, hb'‚ü©
  refine ‚ü®f.symm b, mem_image_of_mem _ hb, f.map_rel_iff.1 ?_‚ü©
  rwa [RelIso.apply_symm_apply]

theorem cof_eq_lift [IsRefl Œ≤ s] (f : r ‚âÉr s) :
    Cardinal.lift.{v} (Order.cof r) = Cardinal.lift.{u} (Order.cof s) :=
  have := f.toRelEmbedding.isRefl
  (f.cof_le_lift).antisymm (f.symm.cof_le_lift)

theorem cof_eq {Œ± Œ≤ : Type u} {r : Œ± ‚Üí Œ± ‚Üí Prop} {s} [IsRefl Œ≤ s] (f : r ‚âÉr s) :
    Order.cof r = Order.cof s :=
  lift_inj.1 (f.cof_eq_lift)

end RelIso

/-! ### Cofinality of ordinals -/

namespace Ordinal

/-- Cofinality of an ordinal. This is the smallest cardinal of a subset `S` of the ordinal which is
unbounded, in the sense `‚àÄ a, ‚àÉ b ‚àà S, a ‚â§ b`.

In particular, `cof 0 = 0` and `cof (succ o) = 1`. -/
def cof (o : Ordinal.{u}) : Cardinal.{u} :=
  o.liftOn (fun a ‚Ü¶ Order.cof (swap a.r·∂ú)) fun _ _ ‚ü®f‚ü© ‚Ü¶ f.compl.swap.cof_eq

theorem cof_type (r : Œ± ‚Üí Œ± ‚Üí Prop) [IsWellOrder Œ± r] : (type r).cof = Order.cof (swap r·∂ú) :=
  rfl

theorem cof_type_lt [LinearOrder Œ±] [IsWellOrder Œ± (¬∑ < ¬∑)] :
    (@type Œ± (¬∑ < ¬∑) _).cof = @Order.cof Œ± (¬∑ ‚â§ ¬∑) := by
  rw [cof_type, compl_lt, swap_ge]

theorem cof_eq_cof_toType (o : Ordinal) : o.cof = @Order.cof o.toType (¬∑ ‚â§ ¬∑) := by
  conv_lhs => rw [‚Üê type_toType o, cof_type_lt]

theorem le_cof_type [IsWellOrder Œ± r] {c} : c ‚â§ cof (type r) ‚Üî ‚àÄ S, Unbounded r S ‚Üí c ‚â§ #S :=
  (le_csInf_iff'' (Order.cof_nonempty _)).trans
    ‚ü®fun H S h => H _ ‚ü®S, h, rfl‚ü©, by
      rintro H d ‚ü®S, h, rfl‚ü©
      exact H _ h‚ü©

theorem cof_type_le [IsWellOrder Œ± r] {S : Set Œ±} (h : Unbounded r S) : cof (type r) ‚â§ #S :=
  le_cof_type.1 le_rfl S h

theorem lt_cof_type [IsWellOrder Œ± r] {S : Set Œ±} : #S < cof (type r) ‚Üí Bounded r S := by
  simpa using not_imp_not.2 cof_type_le

theorem cof_eq (r : Œ± ‚Üí Œ± ‚Üí Prop) [IsWellOrder Œ± r] : ‚àÉ S, Unbounded r S ‚àß #S = cof (type r) :=
  csInf_mem (Order.cof_nonempty (swap r·∂ú))

theorem ord_cof_eq (r : Œ± ‚Üí Œ± ‚Üí Prop) [IsWellOrder Œ± r] :
    ‚àÉ S, Unbounded r S ‚àß type (Subrel r (¬∑ ‚àà S)) = (cof (type r)).ord := by
  let ‚ü®S, hS, e‚ü© := cof_eq r
  let ‚ü®s, _, e'‚ü© := Cardinal.ord_eq S
  let T : Set Œ± := { a | ‚àÉ aS : a ‚àà S, ‚àÄ b : S, s b ‚ü®_, aS‚ü© ‚Üí r b a }
  suffices Unbounded r T by
    refine ‚ü®T, this, le_antisymm ?_ (Cardinal.ord_le.2 <| cof_type_le this)‚ü©
    rw [‚Üê e, e']
    refine
      (RelEmbedding.ofMonotone
          (fun a : T =>
            (‚ü®a,
                let ‚ü®aS, _‚ü© := a.2
                aS‚ü© :
              S))
          fun a b h => ?_).ordinal_type_le
    rcases a with ‚ü®a, aS, ha‚ü©
    rcases b with ‚ü®b, bS, hb‚ü©
    change s ‚ü®a, _‚ü© ‚ü®b, _‚ü©
    refine ((trichotomous_of s _ _).resolve_left fun hn => ?_).resolve_left ?_
    ¬∑ exact asymm h (ha _ hn)
    ¬∑ intro e
      injection e with e
      subst b
      exact irrefl _ h
  intro a
  have : { b : S | ¬¨r b a }.Nonempty :=
    let ‚ü®b, bS, ba‚ü© := hS a
    ‚ü®‚ü®b, bS‚ü©, ba‚ü©
  let b := (IsWellFounded.wf : WellFounded s).min _ this
  have ba : ¬¨r b a := IsWellFounded.wf.min_mem _ this
  refine ‚ü®b, ‚ü®b.2, fun c => not_imp_not.1 fun h => ?_‚ü©, ba‚ü©
  rw [show ‚àÄ b : S, (‚ü®b, b.2‚ü© : S) = b by intro b; cases b; rfl]
  exact IsWellFounded.wf.not_lt_min _ this (IsOrderConnected.neg_trans h ba)

/-! ### Cofinality of suprema and least strict upper bounds -/

private theorem card_mem_cof {o} : ‚àÉ (Œπ : _) (f : Œπ ‚Üí Ordinal), lsub.{u, u} f = o ‚àß #Œπ = o.card :=
  ‚ü®_, _, lsub_typein o, mk_toType o‚ü©

/-- The set in the `lsub` characterization of `cof` is nonempty. -/
theorem cof_lsub_def_nonempty (o) :
    { a : Cardinal | ‚àÉ (Œπ : _) (f : Œπ ‚Üí Ordinal), lsub.{u, u} f = o ‚àß #Œπ = a }.Nonempty :=
  ‚ü®_, card_mem_cof‚ü©

theorem cof_eq_sInf_lsub (o : Ordinal.{u}) : cof o =
    sInf { a : Cardinal | ‚àÉ (Œπ : Type u) (f : Œπ ‚Üí Ordinal), lsub.{u, u} f = o ‚àß #Œπ = a } := by
  refine le_antisymm (le_csInf (cof_lsub_def_nonempty o) ?_) (csInf_le' ?_)
  ¬∑ rintro a ‚ü®Œπ, f, hf, rfl‚ü©
    rw [‚Üê type_toType o]
    refine
      (cof_type_le fun a => ?_).trans
        (@mk_le_of_injective _ _
          (fun s : typein ((¬∑ < ¬∑) : o.toType ‚Üí o.toType ‚Üí Prop) ‚Åª¬π' Set.range f =>
            Classical.choose s.prop)
          fun s t hst => by
          let H := congr_arg f hst
          rwa [Classical.choose_spec s.prop, Classical.choose_spec t.prop, typein_inj,
            Subtype.coe_inj] at H)
    have := typein_lt_self a
    simp_rw [‚Üê hf, lt_lsub_iff] at this
    obtain ‚ü®i, hi‚ü© := this
    refine ‚ü®enum (Œ± := o.toType) (¬∑ < ¬∑) ‚ü®f i, ?_‚ü©, ?_, ?_‚ü©
    ¬∑ rw [type_toType, ‚Üê hf]
      apply lt_lsub
    ¬∑ rw [mem_preimage, typein_enum]
      exact mem_range_self i
    ¬∑ rwa [‚Üê typein_le_typein, typein_enum]
  ¬∑ rcases cof_eq (Œ± := o.toType) (¬∑ < ¬∑) with ‚ü®S, hS, hS'‚ü©
    let f : S ‚Üí Ordinal := fun s => typein LT.lt s.val
    refine ‚ü®S, f, le_antisymm (lsub_le fun i => typein_lt_self (o := o) i)
      (le_of_forall_lt fun a ha => ?_), by rwa [type_toType o] at hS'‚ü©
    rw [‚Üê type_toType o] at ha
    rcases hS (enum (¬∑ < ¬∑) ‚ü®a, ha‚ü©) with ‚ü®b, hb, hb'‚ü©
    rw [‚Üê typein_le_typein, typein_enum] at hb'
    exact hb'.trans_lt (lt_lsub.{u, u} f ‚ü®b, hb‚ü©)

@[simp]
theorem lift_cof (o) : Cardinal.lift.{u, v} (cof o) = cof (Ordinal.lift.{u, v} o) := by
  refine inductionOn o fun Œ± r _ ‚Ü¶ ?_
  rw [‚Üê type_uLift, cof_type, cof_type, ‚Üê Cardinal.lift_id'.{v, u} (Order.cof _),
    ‚Üê Cardinal.lift_umax]
  apply RelIso.cof_eq_lift ‚ü®Equiv.ulift.symm, _‚ü©
  simp [swap]

theorem cof_le_card (o) : cof o ‚â§ card o := by
  rw [cof_eq_sInf_lsub]
  exact csInf_le' card_mem_cof

theorem cof_ord_le (c : Cardinal) : c.ord.cof ‚â§ c := by simpa using cof_le_card c.ord

theorem ord_cof_le (o : Ordinal.{u}) : o.cof.ord ‚â§ o :=
  (ord_le_ord.2 (cof_le_card o)).trans (ord_card_le o)

theorem exists_lsub_cof (o : Ordinal) :
    ‚àÉ (Œπ : _) (f : Œπ ‚Üí Ordinal), lsub.{u, u} f = o ‚àß #Œπ = cof o := by
  rw [cof_eq_sInf_lsub]
  exact csInf_mem (cof_lsub_def_nonempty o)

theorem cof_lsub_le {Œπ} (f : Œπ ‚Üí Ordinal) : cof (lsub.{u, u} f) ‚â§ #Œπ := by
  rw [cof_eq_sInf_lsub]
  exact csInf_le' ‚ü®Œπ, f, rfl, rfl‚ü©

theorem cof_lsub_le_lift {Œπ} (f : Œπ ‚Üí Ordinal) :
    cof (lsub.{u, v} f) ‚â§ Cardinal.lift.{v, u} #Œπ := by
  rw [‚Üê mk_uLift.{u, v}]
  convert cof_lsub_le.{max u v} fun i : ULift.{v, u} Œπ => f i.down
  exact
    lsub_eq_of_range_eq.{u, max u v, max u v}
      (Set.ext fun x => ‚ü®fun ‚ü®i, hi‚ü© => ‚ü®ULift.up.{v, u} i, hi‚ü©, fun ‚ü®i, hi‚ü© => ‚ü®_, hi‚ü©‚ü©)

theorem le_cof_iff_lsub {o : Ordinal} {a : Cardinal} :
    a ‚â§ cof o ‚Üî ‚àÄ {Œπ} (f : Œπ ‚Üí Ordinal), lsub.{u, u} f = o ‚Üí a ‚â§ #Œπ := by
  rw [cof_eq_sInf_lsub]
  exact
    (le_csInf_iff'' (cof_lsub_def_nonempty o)).trans
      ‚ü®fun H Œπ f hf => H _ ‚ü®Œπ, f, hf, rfl‚ü©, fun H b ‚ü®Œπ, f, hf, hb‚ü© => by
        rw [‚Üê hb]
        exact H _ hf‚ü©

theorem lsub_lt_ord_lift {Œπ} {f : Œπ ‚Üí Ordinal} {c : Ordinal}
    (hŒπ : Cardinal.lift.{v, u} #Œπ < c.cof)
    (hf : ‚àÄ i, f i < c) : lsub.{u, v} f < c :=
  lt_of_le_of_ne (lsub_le hf) fun h => by
    subst h
    exact (cof_lsub_le_lift.{u, v} f).not_gt hŒπ

theorem lsub_lt_ord {Œπ} {f : Œπ ‚Üí Ordinal} {c : Ordinal} (hŒπ : #Œπ < c.cof) :
    (‚àÄ i, f i < c) ‚Üí lsub.{u, u} f < c :=
  lsub_lt_ord_lift (by rwa [(#Œπ).lift_id])

theorem cof_iSup_le_lift {Œπ} {f : Œπ ‚Üí Ordinal} (H : ‚àÄ i, f i < iSup f) :
    cof (iSup f) ‚â§ Cardinal.lift.{v, u} #Œπ := by
  rw [‚Üê iSup_eq_lsub_iff_lt_iSup] at H
  rw [H]
  exact cof_lsub_le_lift f

theorem cof_iSup_le {Œπ} {f : Œπ ‚Üí Ordinal} (H : ‚àÄ i, f i < iSup f) :
    cof (iSup f) ‚â§ #Œπ := by
  rw [‚Üê (#Œπ).lift_id]
  exact cof_iSup_le_lift H

theorem iSup_lt_ord_lift {Œπ} {f : Œπ ‚Üí Ordinal} {c : Ordinal} (hŒπ : Cardinal.lift.{v, u} #Œπ < c.cof)
    (hf : ‚àÄ i, f i < c) : iSup f < c :=
  (iSup_le_lsub f).trans_lt (lsub_lt_ord_lift hŒπ hf)

theorem iSup_lt_ord {Œπ} {f : Œπ ‚Üí Ordinal} {c : Ordinal} (hŒπ : #Œπ < c.cof) :
    (‚àÄ i, f i < c) ‚Üí iSup f < c :=
  iSup_lt_ord_lift (by rwa [(#Œπ).lift_id])

theorem iSup_lt_lift {Œπ} {f : Œπ ‚Üí Cardinal} {c : Cardinal}
    (hŒπ : Cardinal.lift.{v, u} #Œπ < c.ord.cof)
    (hf : ‚àÄ i, f i < c) : iSup f < c := by
  rw [‚Üê ord_lt_ord, iSup_ord]
  refine iSup_lt_ord_lift hŒπ fun i => ?_
  rw [ord_lt_ord]
  apply hf

theorem iSup_lt {Œπ} {f : Œπ ‚Üí Cardinal} {c : Cardinal} (hŒπ : #Œπ < c.ord.cof) :
    (‚àÄ i, f i < c) ‚Üí iSup f < c :=
  iSup_lt_lift (by rwa [(#Œπ).lift_id])

theorem nfpFamily_lt_ord_lift {Œπ} {f : Œπ ‚Üí Ordinal ‚Üí Ordinal} {c} (hc : ‚Ñµ‚ÇÄ < cof c)
    (hc' : Cardinal.lift.{v, u} #Œπ < cof c) (hf : ‚àÄ (i), ‚àÄ b < c, f i b < c) {a} (ha : a < c) :
    nfpFamily f a < c := by
  refine iSup_lt_ord_lift ((Cardinal.lift_le.2 (mk_list_le_max Œπ)).trans_lt ?_) fun l => ?_
  ¬∑ rw [lift_max]
    apply max_lt _ hc'
    rwa [Cardinal.lift_aleph0]
  ¬∑ induction l with
    | nil => exact ha
    | cons i l H => exact hf _ _ H

theorem nfpFamily_lt_ord {Œπ} {f : Œπ ‚Üí Ordinal ‚Üí Ordinal} {c} (hc : ‚Ñµ‚ÇÄ < cof c) (hc' : #Œπ < cof c)
    (hf : ‚àÄ (i), ‚àÄ b < c, f i b < c) {a} : a < c ‚Üí nfpFamily.{u, u} f a < c :=
  nfpFamily_lt_ord_lift hc (by rwa [(#Œπ).lift_id]) hf

theorem nfp_lt_ord {f : Ordinal ‚Üí Ordinal} {c} (hc : ‚Ñµ‚ÇÄ < cof c) (hf : ‚àÄ i < c, f i < c) {a} :
    a < c ‚Üí nfp f a < c :=
  nfpFamily_lt_ord_lift hc (by simpa using Cardinal.one_lt_aleph0.trans hc) fun _ => hf

theorem exists_blsub_cof (o : Ordinal) :
    ‚àÉ f : ‚àÄ a < (cof o).ord, Ordinal, blsub.{u, u} _ f = o := by
  rcases exists_lsub_cof o with ‚ü®Œπ, f, hf, hŒπ‚ü©
  rcases Cardinal.ord_eq Œπ with ‚ü®r, hr, hŒπ'‚ü©
  rw [‚Üê @blsub_eq_lsub' Œπ r hr] at hf
  rw [‚Üê hŒπ, hŒπ']
  exact ‚ü®_, hf‚ü©

theorem le_cof_iff_blsub {b : Ordinal} {a : Cardinal} :
    a ‚â§ cof b ‚Üî ‚àÄ {o} (f : ‚àÄ a < o, Ordinal), blsub.{u, u} o f = b ‚Üí a ‚â§ o.card :=
  le_cof_iff_lsub.trans
    ‚ü®fun H o f hf => by simpa using H _ hf, fun H Œπ f hf => by
      rcases Cardinal.ord_eq Œπ with ‚ü®r, hr, hŒπ'‚ü©
      rw [‚Üê @blsub_eq_lsub' Œπ r hr] at hf
      simpa using H _ hf‚ü©

theorem cof_blsub_le_lift {o} (f : ‚àÄ a < o, Ordinal) :
    cof (blsub.{u, v} o f) ‚â§ Cardinal.lift.{v, u} o.card := by
  rw [‚Üê mk_toType o]
  exact cof_lsub_le_lift _

theorem cof_blsub_le {o} (f : ‚àÄ a < o, Ordinal) : cof (blsub.{u, u} o f) ‚â§ o.card := by
  rw [‚Üê o.card.lift_id]
  exact cof_blsub_le_lift f

theorem blsub_lt_ord_lift {o : Ordinal.{u}} {f : ‚àÄ a < o, Ordinal} {c : Ordinal}
    (ho : Cardinal.lift.{v, u} o.card < c.cof) (hf : ‚àÄ i hi, f i hi < c) : blsub.{u, v} o f < c :=
  lt_of_le_of_ne (blsub_le hf) fun h =>
    ho.not_ge (by simpa [‚Üê iSup_ord, hf, h] using cof_blsub_le_lift.{u, v} f)

theorem blsub_lt_ord {o : Ordinal} {f : ‚àÄ a < o, Ordinal} {c : Ordinal} (ho : o.card < c.cof)
    (hf : ‚àÄ i hi, f i hi < c) : blsub.{u, u} o f < c :=
  blsub_lt_ord_lift (by rwa [o.card.lift_id]) hf

theorem cof_bsup_le_lift {o : Ordinal} {f : ‚àÄ a < o, Ordinal} (H : ‚àÄ i h, f i h < bsup.{u, v} o f) :
    cof (bsup.{u, v} o f) ‚â§ Cardinal.lift.{v, u} o.card := by
  rw [‚Üê bsup_eq_blsub_iff_lt_bsup.{u, v}] at H
  rw [H]
  exact cof_blsub_le_lift.{u, v} f

theorem cof_bsup_le {o : Ordinal} {f : ‚àÄ a < o, Ordinal} :
    (‚àÄ i h, f i h < bsup.{u, u} o f) ‚Üí cof (bsup.{u, u} o f) ‚â§ o.card := by
  rw [‚Üê o.card.lift_id]
  exact cof_bsup_le_lift

theorem bsup_lt_ord_lift {o : Ordinal} {f : ‚àÄ a < o, Ordinal} {c : Ordinal}
    (ho : Cardinal.lift.{v, u} o.card < c.cof) (hf : ‚àÄ i hi, f i hi < c) : bsup.{u, v} o f < c :=
  (bsup_le_blsub f).trans_lt (blsub_lt_ord_lift ho hf)

theorem bsup_lt_ord {o : Ordinal} {f : ‚àÄ a < o, Ordinal} {c : Ordinal} (ho : o.card < c.cof) :
    (‚àÄ i hi, f i hi < c) ‚Üí bsup.{u, u} o f < c :=
  bsup_lt_ord_lift (by rwa [o.card.lift_id])

/-! ### Basic results -/

@[simp]
theorem cof_zero : cof 0 = 0 := by
  refine LE.le.antisymm  ?_ (Cardinal.zero_le _)
  rw [‚Üê card_zero]
  exact cof_le_card 0

@[simp]
theorem cof_eq_zero {o} : cof o = 0 ‚Üî o = 0 :=
  ‚ü®inductionOn o fun _ r _ z =>
      let ‚ü®_, hl, e‚ü© := cof_eq r
      type_eq_zero_iff_isEmpty.2 <|
        ‚ü®fun a =>
          let ‚ü®_, h, _‚ü© := hl a
          (mk_eq_zero_iff.1 (e.trans z)).elim' ‚ü®_, h‚ü©‚ü©,
    fun e => by simp [e]‚ü©

theorem cof_ne_zero {o} : cof o ‚â† 0 ‚Üî o ‚â† 0 :=
  cof_eq_zero.not

@[simp]
theorem cof_succ (o) : cof (succ o) = 1 := by
  apply le_antisymm
  ¬∑ refine inductionOn o fun Œ± r _ => ?_
    change cof (type _) ‚â§ _
    rw [‚Üê (_ : #_ = 1)]
    ¬∑ apply cof_type_le
      refine fun a => ‚ü®Sum.inr PUnit.unit, Set.mem_singleton _, ?_‚ü©
      rcases a with (a | ‚ü®‚ü®‚ü®‚ü©‚ü©‚ü©) <;> simp [EmptyRelation]
    ¬∑ simp
  ¬∑ rw [‚Üê Cardinal.succ_zero, succ_le_iff]
    simpa [lt_iff_le_and_ne, Cardinal.zero_le] using fun h =>
      succ_ne_zero o (cof_eq_zero.1 (Eq.symm h))

@[simp]
theorem cof_eq_one_iff_is_succ {o} : cof.{u} o = 1 ‚Üî ‚àÉ a, o = succ a :=
  ‚ü®inductionOn o fun Œ± r _ z => by
      rcases cof_eq r with ‚ü®S, hl, e‚ü©; rw [z] at e
      obtain ‚ü®a‚ü© := mk_ne_zero_iff.1 (by rw [e]; exact one_ne_zero)
      refine
        ‚ü®typein r a,
          Eq.symm <|
            Quotient.sound
              ‚ü®RelIso.ofSurjective (RelEmbedding.ofMonotone ?_ fun x y => ?_) fun x => ?_‚ü©‚ü©
      ¬∑ apply Sum.rec <;> [exact Subtype.val; exact fun _ => a]
      ¬∑ rcases x with (x | ‚ü®‚ü®‚ü®‚ü©‚ü©‚ü©) <;> rcases y with (y | ‚ü®‚ü®‚ü®‚ü©‚ü©‚ü©) <;>
          simp [Subrel, Order.Preimage, EmptyRelation]
        exact x.2
      ¬∑ suffices r x a ‚à® ‚àÉ _ : PUnit.{u}, ‚Üëa = x by
          convert this
          dsimp [RelEmbedding.ofMonotone]; simp
        rcases trichotomous_of r x a with (h | h | h)
        ¬∑ exact Or.inl h
        ¬∑ exact Or.inr ‚ü®PUnit.unit, h.symm‚ü©
        ¬∑ rcases hl x with ‚ü®a', aS, hn‚ü©
          refine absurd h ?_
          convert hn
          change (a : Œ±) = ‚Üë(‚ü®a', aS‚ü© : S)
          have := le_one_iff_subsingleton.1 (le_of_eq e)
          congr!,
    fun ‚ü®a, e‚ü© => by simp [e]‚ü©

/-! ### Fundamental sequences -/

-- TODO: move stuff about fundamental sequences to their own file.

/-- A fundamental sequence for `a` is an increasing sequence of length `o = cof a` that converges at
    `a`. We provide `o` explicitly in order to avoid type rewrites. -/
def IsFundamentalSequence (a o : Ordinal.{u}) (f : ‚àÄ b < o, Ordinal.{u}) : Prop :=
  o ‚â§ a.cof.ord ‚àß (‚àÄ {i j} (hi hj), i < j ‚Üí f i hi < f j hj) ‚àß blsub.{u, u} o f = a

namespace IsFundamentalSequence

variable {a o : Ordinal.{u}} {f : ‚àÄ b < o, Ordinal.{u}}

protected theorem cof_eq (hf : IsFundamentalSequence a o f) : a.cof.ord = o :=
  hf.1.antisymm' <| by
    rw [‚Üê hf.2.2]
    exact (ord_le_ord.2 (cof_blsub_le f)).trans (ord_card_le o)

protected theorem strict_mono (hf : IsFundamentalSequence a o f) {i j} :
    ‚àÄ hi hj, i < j ‚Üí f i hi < f j hj :=
  hf.2.1

theorem blsub_eq (hf : IsFundamentalSequence a o f) : blsub.{u, u} o f = a :=
  hf.2.2

theorem ord_cof (hf : IsFundamentalSequence a o f) :
    IsFundamentalSequence a a.cof.ord fun i hi => f i (hi.trans_le (by rw [hf.cof_eq])) := by
  have H := hf.cof_eq
  subst H
  exact hf

theorem id_of_le_cof (h : o ‚â§ o.cof.ord) : IsFundamentalSequence o o fun a _ => a :=
  ‚ü®h, @fun _ _ _ _ => id, blsub_id o‚ü©

protected theorem zero {f : ‚àÄ b < (0 : Ordinal), Ordinal} : IsFundamentalSequence 0 0 f :=
  ‚ü®by rw [cof_zero, ord_zero], @fun i _ hi => (Ordinal.not_lt_zero i hi).elim, blsub_zero f‚ü©

protected theorem succ : IsFundamentalSequence (succ o) 1 fun _ _ => o := by
  refine ‚ü®?_, @fun i j hi hj h => ?_, blsub_const Ordinal.one_ne_zero o‚ü©
  ¬∑ rw [cof_succ, ord_one]
  ¬∑ rw [lt_one_iff_zero] at hi hj
    rw [hi, hj] at h
    exact h.false.elim

protected theorem monotone (hf : IsFundamentalSequence a o f) {i j : Ordinal} (hi : i < o)
    (hj : j < o) (hij : i ‚â§ j) : f i hi ‚â§ f j hj := by
  rcases lt_or_eq_of_le hij with (hij | rfl)
  ¬∑ exact (hf.2.1 hi hj hij).le
  ¬∑ rfl

theorem trans {a o o' : Ordinal.{u}} {f : ‚àÄ b < o, Ordinal.{u}} (hf : IsFundamentalSequence a o f)
    {g : ‚àÄ b < o', Ordinal.{u}} (hg : IsFundamentalSequence o o' g) :
    IsFundamentalSequence a o' fun i hi =>
      f (g i hi) (by rw [‚Üê hg.2.2]; apply lt_blsub) := by
  refine ‚ü®?_, @fun i j _ _ h => hf.2.1 _ _ (hg.2.1 _ _ h), ?_‚ü©
  ¬∑ rw [hf.cof_eq]
    exact hg.1.trans (ord_cof_le o)
  ¬∑ rw [@blsub_comp.{u, u, u} o _ f (@IsFundamentalSequence.monotone _ _ f hf)]
    ¬∑ exact hf.2.2
    ¬∑ exact hg.2.2

protected theorem lt {a o : Ordinal} {s : Œ† p < o, Ordinal}
    (h : IsFundamentalSequence a o s) {p : Ordinal} (hp : p < o) : s p hp < a :=
  h.blsub_eq ‚ñ∏ lt_blsub s p hp

end IsFundamentalSequence

/-- Every ordinal has a fundamental sequence. -/
theorem exists_fundamental_sequence (a : Ordinal.{u}) :
    ‚àÉ f, IsFundamentalSequence a a.cof.ord f := by
  suffices h : ‚àÉ o f, IsFundamentalSequence a o f by
    rcases h with ‚ü®o, f, hf‚ü©
    exact ‚ü®_, hf.ord_cof‚ü©
  rcases exists_lsub_cof a with ‚ü®Œπ, f, hf, hŒπ‚ü©
  rcases ord_eq Œπ with ‚ü®r, wo, hr‚ü©
  let r' := Subrel r fun i ‚Ü¶ ‚àÄ j, r j i ‚Üí f j < f i
  let hrr' : r' ‚Ü™r r := Subrel.relEmbedding _ _
  haveI := hrr'.isWellOrder
  refine
    ‚ü®_, _, hrr'.ordinal_type_le.trans ?_, @fun i j _ h _ => (enum r' ‚ü®j, h‚ü©).prop _ ?_,
      le_antisymm (blsub_le fun i hi => lsub_le_iff.1 hf.le _) ?_‚ü©
  ¬∑ rw [‚Üê hŒπ, hr]
  ¬∑ change r (hrr'.1 _) (hrr'.1 _)
    rwa [hrr'.2, @enum_lt_enum _ r']
  ¬∑ rw [‚Üê hf, lsub_le_iff]
    intro i
    suffices h : ‚àÉ i' hi', f i ‚â§ bfamilyOfFamily' r' (fun i => f i) i' hi' by
      rcases h with ‚ü®i', hi', hfg‚ü©
      exact hfg.trans_lt (lt_blsub _ _ _)
    by_cases h : ‚àÄ j, r j i ‚Üí f j < f i
    ¬∑ refine ‚ü®typein r' ‚ü®i, h‚ü©, typein_lt_type _ _, ?_‚ü©
      rw [bfamilyOfFamily'_typein]
    ¬∑ push_neg at h
      obtain ‚ü®hji, hij‚ü© := wo.wf.min_mem _ h
      refine ‚ü®typein r' ‚ü®_, fun k hkj => lt_of_lt_of_le ?_ hij‚ü©, typein_lt_type _ _, ?_‚ü©
      ¬∑ by_contra! H
        exact (wo.wf.not_lt_min _ h ‚ü®IsTrans.trans _ _ _ hkj hji, H‚ü©) hkj
      ¬∑ rwa [bfamilyOfFamily'_typein]

@[simp]
theorem cof_cof (a : Ordinal.{u}) : cof (cof a).ord = cof a := by
  obtain ‚ü®f, hf‚ü© := exists_fundamental_sequence a
  obtain ‚ü®g, hg‚ü© := exists_fundamental_sequence a.cof.ord
  exact ord_injective (hf.trans hg).cof_eq.symm

protected theorem IsNormal.isFundamentalSequence {f : Ordinal.{u} ‚Üí Ordinal.{u}} (hf : IsNormal f)
    {a o} (ha : IsSuccLimit a) {g} (hg : IsFundamentalSequence a o g) :
    IsFundamentalSequence (f a) o fun b hb => f (g b hb) := by
  refine ‚ü®?_, @fun i j _ _ h => hf.strictMono (hg.2.1 _ _ h), ?_‚ü©
  ¬∑ rcases exists_lsub_cof (f a) with ‚ü®Œπ, f', hf', hŒπ‚ü©
    rw [‚Üê hg.cof_eq, ord_le_ord, ‚Üê hŒπ]
    suffices (lsub.{u, u} fun i => sInf { b : Ordinal | f' i ‚â§ f b }) = a by
      rw [‚Üê this]
      apply cof_lsub_le
    have H : ‚àÄ i, ‚àÉ b < a, f' i ‚â§ f b := fun i => by
      have := lt_lsub.{u, u} f' i
      rw [hf', ‚Üê IsNormal.blsub_eq.{u, u} hf ha, lt_blsub_iff] at this
      simpa using this
    refine (lsub_le fun i => ?_).antisymm (le_of_forall_lt fun b hb => ?_)
    ¬∑ rcases H i with ‚ü®b, hb, hb'‚ü©
      exact lt_of_le_of_lt (csInf_le' hb') hb
    ¬∑ have := hf.strictMono hb
      rw [‚Üê hf', lt_lsub_iff] at this
      obtain ‚ü®i, hi‚ü© := this
      rcases H i with ‚ü®b, _, hb‚ü©
      exact
        ((le_csInf_iff'' ‚ü®b, by exact hb‚ü©).2 fun c hc =>
          hf.strictMono.le_iff_le.1 (hi.trans hc)).trans_lt (lt_lsub _ i)
  ¬∑ rw [@blsub_comp.{u, u, u} a _ (fun b _ => f b) (@fun i j _ _ h => hf.strictMono.monotone h) g
        hg.2.2]
    exact IsNormal.blsub_eq.{u, u} hf ha

theorem IsNormal.cof_eq {f} (hf : IsNormal f) {a} (ha : IsSuccLimit a) : cof (f a) = cof a :=
  let ‚ü®_, hg‚ü© := exists_fundamental_sequence a
  ord_injective (hf.isFundamentalSequence ha hg).cof_eq

theorem IsNormal.cof_le {f} (hf : IsNormal f) (a) : cof a ‚â§ cof (f a) := by
  rcases zero_or_succ_or_isSuccLimit a with (rfl | ‚ü®b, rfl‚ü© | ha)
  ¬∑ rw [cof_zero]
    exact zero_le _
  ¬∑ rw [cof_succ, Cardinal.one_le_iff_ne_zero, cof_ne_zero, ‚Üê Ordinal.pos_iff_ne_zero]
    exact (Ordinal.zero_le (f b)).trans_lt (hf.strictMono (lt_succ b))
  ¬∑ rw [hf.cof_eq ha]

@[simp]
theorem cof_add (a b : Ordinal) : b ‚â† 0 ‚Üí cof (a + b) = cof b := fun h => by
  rcases zero_or_succ_or_isSuccLimit b with (rfl | ‚ü®c, rfl‚ü© | hb)
  ¬∑ contradiction
  ¬∑ rw [add_succ, cof_succ, cof_succ]
  ¬∑ exact (isNormal_add_right a).cof_eq hb

theorem aleph0_le_cof {o} : ‚Ñµ‚ÇÄ ‚â§ cof o ‚Üî IsSuccLimit o := by
  rcases zero_or_succ_or_isSuccLimit o with (rfl | ‚ü®o, rfl‚ü© | l)
  ¬∑ simp [Cardinal.aleph0_ne_zero]
  ¬∑ simp [Cardinal.one_lt_aleph0]
  ¬∑ simp only [l, iff_true]
    refine le_of_not_gt fun h => ?_
    obtain ‚ü®n, e‚ü© := Cardinal.lt_aleph0.1 h
    have := cof_cof o
    rw [e, ord_nat] at this
    cases n
    ¬∑ apply l.ne_bot
      simpa using e
    ¬∑ rw [natCast_succ, cof_succ] at this
      rw [‚Üê this, cof_eq_one_iff_is_succ] at e
      rcases e with ‚ü®a, rfl‚ü©
      exact not_isSuccLimit_succ _ l

@[simp]
theorem cof_preOmega {o : Ordinal} (ho : IsSuccPrelimit o) : (preOmega o).cof = o.cof := by
  by_cases h : IsMin o
  ¬∑ simp [h.eq_bot]
  ¬∑ exact isNormal_preOmega.cof_eq ‚ü®h, ho‚ü©

@[simp]
theorem cof_omega {o : Ordinal} (ho : IsSuccLimit o) : (œâ_ o).cof = o.cof :=
  isNormal_omega.cof_eq ho

@[simp]
theorem cof_omega0 : cof œâ = ‚Ñµ‚ÇÄ :=
  (aleph0_le_cof.2 isSuccLimit_omega0).antisymm' <| by
    rw [‚Üê card_omega0]
    apply cof_le_card

theorem cof_eq' (r : Œ± ‚Üí Œ± ‚Üí Prop) [IsWellOrder Œ± r] (h : IsSuccLimit (type r)) :
    ‚àÉ S : Set Œ±, (‚àÄ a, ‚àÉ b ‚àà S, r a b) ‚àß #S = cof (type r) :=
  let ‚ü®S, H, e‚ü© := cof_eq r
  ‚ü®S, fun a =>
    let a' := enum r ‚ü®_, h.succ_lt (typein_lt_type r a)‚ü©
    let ‚ü®b, h, ab‚ü© := H a'
    ‚ü®b, h,
      (IsOrderConnected.conn a b a' <|
            (typein_lt_typein r).1
              (by
                rw [typein_enum]
                exact lt_succ (typein _ _))).resolve_right
        ab‚ü©,
    e‚ü©

@[simp]
theorem cof_univ : cof univ.{u, v} = Cardinal.univ.{u, v} :=
  le_antisymm (cof_le_card _)
    (by
      refine le_of_forall_lt fun c h => ?_
      rcases lt_univ'.1 h with ‚ü®c, rfl‚ü©
      rcases @cof_eq Ordinal.{u} (¬∑ < ¬∑) _ with ‚ü®S, H, Se‚ü©
      rw [univ, ‚Üê lift_cof, ‚Üê Cardinal.lift_lift.{u+1, v, u}, Cardinal.lift_lt, ‚Üê Se]
      refine lt_of_not_ge fun h => ?_
      obtain ‚ü®a, e‚ü© := Cardinal.mem_range_lift_of_le h
      refine Quotient.inductionOn a (fun Œ± e => ?_) e
      obtain ‚ü®f‚ü© := Quotient.exact e
      have f := Equiv.ulift.symm.trans f
      let g a := (f a).1
      let o := succ (iSup g)
      rcases H o with ‚ü®b, h, l‚ü©
      refine l (lt_succ_iff.2 ?_)
      rw [‚Üê show g (f.symm ‚ü®b, h‚ü©) = b by simp [g]]
      apply Ordinal.le_iSup)

end Ordinal

namespace Cardinal
open Ordinal

/-! ### Results on sets -/

theorem mk_bounded_subset {Œ± : Type*} (h : ‚àÄ x < #Œ±, 2 ^ x < #Œ±) {r : Œ± ‚Üí Œ± ‚Üí Prop}
    [IsWellOrder Œ± r] (hr : (#Œ±).ord = type r) : #{ s : Set Œ± // Bounded r s } = #Œ± := by
  rcases eq_or_ne #Œ± 0 with (ha | ha)
  ¬∑ rw [ha]
    haveI := mk_eq_zero_iff.1 ha
    rw [mk_eq_zero_iff]
    constructor
    rintro ‚ü®s, hs‚ü©
    exact (not_unbounded_iff s).2 hs (unbounded_of_isEmpty s)
  have h' : IsStrongLimit #Œ± := ‚ü®ha, @h‚ü©
  have ha := h'.aleph0_le
  apply le_antisymm
  ¬∑ have : { s : Set Œ± | Bounded r s } = ‚ãÉ i, ùí´{ j | r j i } := setOf_exists _
    rw [‚Üê coe_setOf, this]
    refine mk_iUnion_le_sum_mk.trans ((sum_le_iSup (fun i => #(ùí´{ j | r j i }))).trans
      ((mul_le_max_of_aleph0_le_left ha).trans ?_))
    rw [max_eq_left]
    apply ciSup_le' _
    intro i
    rw [mk_powerset]
    apply (h'.two_power_lt _).le
    rw [coe_setOf, card_typein, ‚Üê lt_ord, hr]
    apply typein_lt_type
  ¬∑ refine @mk_le_of_injective Œ± _ (fun x => Subtype.mk {x} ?_) ?_
    ¬∑ apply bounded_singleton
      rw [‚Üê hr]
      apply isSuccLimit_ord ha
    ¬∑ intro a b hab
      simpa [singleton_eq_singleton_iff] using hab

theorem mk_subset_mk_lt_cof {Œ± : Type*} (h : ‚àÄ x < #Œ±, 2 ^ x < #Œ±) :
    #{ s : Set Œ± // #s < cof (#Œ±).ord } = #Œ± := by
  rcases eq_or_ne #Œ± 0 with (ha | ha)
  ¬∑ simp [ha]
  have h' : IsStrongLimit #Œ± := ‚ü®ha, @h‚ü©
  rcases ord_eq Œ± with ‚ü®r, wo, hr‚ü©
  apply le_antisymm
  ¬∑ conv_rhs => rw [‚Üê mk_bounded_subset h hr]
    apply mk_le_mk_of_subset
    intro s hs
    rw [hr] at hs
    exact lt_cof_type hs
  ¬∑ refine @mk_le_of_injective Œ± _ (fun x => Subtype.mk {x} ?_) ?_
    ¬∑ rw [mk_singleton]
      exact one_lt_aleph0.trans_le (aleph0_le_cof.2 (isSuccLimit_ord h'.aleph0_le))
    ¬∑ intro a b hab
      simpa [singleton_eq_singleton_iff] using hab

/-- If the union of s is unbounded and s is smaller than the cofinality,
  then s has an unbounded member -/
theorem unbounded_of_unbounded_sUnion (r : Œ± ‚Üí Œ± ‚Üí Prop) [wo : IsWellOrder Œ± r] {s : Set (Set Œ±)}
    (h‚ÇÅ : Unbounded r <| ‚ãÉ‚ÇÄ s) (h‚ÇÇ : #s < Order.cof (swap r·∂ú)) : ‚àÉ x ‚àà s, Unbounded r x := by
  by_contra! h
  simp_rw [not_unbounded_iff] at h
  let f : s ‚Üí Œ± := fun x : s => wo.wf.sup x (h x.1 x.2)
  refine h‚ÇÇ.not_ge (le_trans (csInf_le' ‚ü®range f, fun x => ?_, rfl‚ü©) mk_range_le)
  rcases h‚ÇÅ x with ‚ü®y, ‚ü®c, hc, hy‚ü©, hxy‚ü©
  exact ‚ü®f ‚ü®c, hc‚ü©, mem_range_self _, fun hxz => hxy (Trans.trans (wo.wf.lt_sup _ hy) hxz)‚ü©

/-- If the union of s is unbounded and s is smaller than the cofinality,
  then s has an unbounded member -/
theorem unbounded_of_unbounded_iUnion {Œ± Œ≤ : Type u} (r : Œ± ‚Üí Œ± ‚Üí Prop) [wo : IsWellOrder Œ± r]
    (s : Œ≤ ‚Üí Set Œ±) (h‚ÇÅ : Unbounded r <| ‚ãÉ x, s x) (h‚ÇÇ : #Œ≤ < Order.cof (swap r·∂ú)) :
    ‚àÉ x : Œ≤, Unbounded r (s x) := by
  rw [‚Üê sUnion_range] at h‚ÇÅ
  rcases unbounded_of_unbounded_sUnion r h‚ÇÅ (mk_range_le.trans_lt h‚ÇÇ) with ‚ü®_, ‚ü®x, rfl‚ü©, u‚ü©
  exact ‚ü®x, u‚ü©

/-! ### Consequences of K√∂nig's lemma -/

theorem lt_power_cof {c : Cardinal.{u}} : ‚Ñµ‚ÇÄ ‚â§ c ‚Üí c < c ^ c.ord.cof :=
  Cardinal.inductionOn c fun Œ± h => by
    rcases ord_eq Œ± with ‚ü®r, wo, re‚ü©
    have := isSuccLimit_ord h
    rw [re] at this ‚ä¢
    rcases cof_eq' r this with ‚ü®S, H, Se‚ü©
    have := sum_lt_prod (fun a : S => #{ x // r x a }) (fun _ => #Œ±) fun i => ?_
    ¬∑ simp only [Cardinal.prod_const, Cardinal.lift_id, ‚Üê Se, ‚Üê mk_sigma, power_def] at this ‚ä¢
      refine lt_of_le_of_lt ?_ this
      refine ‚ü®Embedding.ofSurjective ?_ ?_‚ü©
      ¬∑ exact fun x => x.2.1
      ¬∑ exact fun a =>
          let ‚ü®b, h, ab‚ü© := H a
          ‚ü®‚ü®‚ü®_, h‚ü©, _, ab‚ü©, rfl‚ü©
    ¬∑ have := typein_lt_type r i
      rwa [‚Üê re, lt_ord] at this

theorem lt_cof_power {a b : Cardinal} (ha : ‚Ñµ‚ÇÄ ‚â§ a) (b1 : 1 < b) : a < (b ^ a).ord.cof := by
  have b0 : b ‚â† 0 := (zero_lt_one.trans b1).ne'
  apply lt_imp_lt_of_le_imp_le (power_le_power_left <| power_ne_zero a b0)
  rw [‚Üê power_mul, mul_eq_self ha]
  exact lt_power_cof (ha.trans <| (cantor' _ b1).le)

end Cardinal
