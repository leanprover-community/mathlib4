/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/

import Mathlib.Data.Fintype.Basic
import Mathlib.SetTheory.Cardinal.Cofinality
import Mathlib.SetTheory.Game.Birthday

#align_import set_theory.game.short from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Short games

A combinatorial game is `Short` [Conway, ch.9][conway2001] if it has only finitely many positions.
In particular, this means there is a finite set of moves at every point.

We prove that the order relations `‚â§` and `<`, and the equivalence relation `‚âà`, are decidable on
short games, although unfortunately in practice `decide` doesn't seem to be able to
prove anything using these instances.
-/

-- Porting note: The local instances `moveLeftShort'` and `fintypeLeft` (and resp. `Right`)
-- trigger this error.
set_option synthInstance.checkSynthOrder false

universe u

open scoped PGame

namespace PGame

/-- A short game is a game with a finite set of moves at every turn. -/
inductive Short : PGame.{u} ‚Üí Type (u + 1)
  | mk :
    ‚àÄ {Œ± Œ≤ : Type u} {L : Œ± ‚Üí PGame.{u}} {R : Œ≤ ‚Üí PGame.{u}} (_ : ‚àÄ i : Œ±, Short (L i))
      (_ : ‚àÄ j : Œ≤, Short (R j)) [Fintype Œ±] [Fintype Œ≤], Short ‚ü®Œ±, Œ≤, L, R‚ü©
#align pgame.short PGame.Short

-- Porting note: Added `simpNF` exception. It's unclear what puts `eq_iff_true_of_subsingleton` into
-- the simp set. A minimal reproduction of the simpNF error needs to import transitively at least
-- `Mathlib.Logic.Unique`.
--
-- The simplifier can already prove this using `eq_iff_true_of_subsingleton`
attribute [nolint simpNF] Short.mk.injEq

instance subsingleton_short (x : PGame) : Subsingleton (Short x) := by
  induction x with
  | mk xl xr xL xR =>
    constructor
    intro a b
    cases a; cases b
    congr!


-- Porting note: We use `induction` to prove `subsingleton_short` instead of recursion.
-- A proof using recursion generates a harder `decreasing_by` goal than in Lean 3 for some reason:
attribute [-instance] subsingleton_short in
theorem subsingleton_short_example : ‚àÄ x : PGame, Subsingleton (Short x)
  | mk xl xr xL xR =>
    ‚ü®fun a b => by
      cases a; cases b
      -- ‚ä¢ Short.mk x‚úù¬π x‚úù = b
               -- ‚ä¢ Short.mk x‚úù¬≥ x‚úù¬≤ = Short.mk x‚úù¬π x‚úù
      congr!
      -- ‚ä¢ x‚úù¬≥ = x‚úù¬π
      ¬∑ funext x
        -- ‚ä¢ x‚úù¬≥ x = x‚úù¬π x
        apply @Subsingleton.elim _ (subsingleton_short_example (xL x))
        -- üéâ no goals
        -- Decreasing goal in Lean 4 is `Subsequent (xL x) (mk Œ± Œ≤ L R)`
        -- where `Œ±`, `Œ≤`, `L`, and `R` are fresh hypotheses only propositionally
        -- equal to `xl`, `xr`, `xL`, and `xR`.
        -- (In Lean 3 it was `(mk xl xr xL xR)` instead.)
      ¬∑ funext x
        -- ‚ä¢ x‚úù¬≤ x = x‚úù x
        apply @Subsingleton.elim _ (subsingleton_short_example (xR x))‚ü©
        -- üéâ no goals
termination_by subsingleton_short_example x => x
-- We need to unify a bunch of hypotheses before `pgame_wf_tac` can work.
decreasing_by {
  subst_vars
  simp only [mk.injEq, heq_eq_eq, true_and] at *
  casesm* _ ‚àß _
  subst_vars
  pgame_wf_tac
}

#align pgame.subsingleton_short PGame.subsingleton_short

/-- A synonym for `Short.mk` that specifies the pgame in an implicit argument. -/
def Short.mk' {x : PGame} [Fintype x.LeftMoves] [Fintype x.RightMoves]
    (sL : ‚àÄ i : x.LeftMoves, Short (x.moveLeft i))
    (sR : ‚àÄ j : x.RightMoves, Short (x.moveRight j)) : Short x := by
  -- Porting note: Old proof relied on `unfreezingI`, which doesn't exist in Lean 4.
  convert Short.mk sL sR
  -- ‚ä¢ x = PGame.mk (LeftMoves x) (RightMoves x) (fun i => moveLeft x i) fun j => m ‚Ä¶
  cases x
  -- ‚ä¢ PGame.mk Œ±‚úù Œ≤‚úù a‚úù¬π a‚úù = PGame.mk (LeftMoves (PGame.mk Œ±‚úù Œ≤‚úù a‚úù¬π a‚úù)) (RightM ‚Ä¶
  dsimp
  -- üéâ no goals
#align pgame.short.mk' PGame.Short.mk'

attribute [class] Short

/-- Extracting the `Fintype` instance for the indexing type for Left's moves in a short game.
This is an unindexed typeclass, so it can't be made a global instance.
-/
def fintypeLeft {Œ± Œ≤ : Type u} {L : Œ± ‚Üí PGame.{u}} {R : Œ≤ ‚Üí PGame.{u}} [S : Short ‚ü®Œ±, Œ≤, L, R‚ü©] :
    Fintype Œ± := by cases' S with _ _ _ _ _ _ F _; exact F
                    -- ‚ä¢ Fintype Œ±
                                                   -- üéâ no goals
#align pgame.fintype_left PGame.fintypeLeft

attribute [local instance] fintypeLeft

instance fintypeLeftMoves (x : PGame) [S : Short x] : Fintype x.LeftMoves := by
  cases S; assumption
  -- ‚ä¢ Fintype (LeftMoves (mk Œ±‚úù Œ≤‚úù L‚úù R‚úù))
           -- üéâ no goals
#align pgame.fintype_left_moves PGame.fintypeLeftMoves

/-- Extracting the `Fintype` instance for the indexing type for Right's moves in a short game.
This is an unindexed typeclass, so it can't be made a global instance.
-/
def fintypeRight {Œ± Œ≤ : Type u} {L : Œ± ‚Üí PGame.{u}} {R : Œ≤ ‚Üí PGame.{u}} [S : Short ‚ü®Œ±, Œ≤, L, R‚ü©] :
    Fintype Œ≤ := by cases' S with _ _ _ _ _ _ _ F; exact F
                    -- ‚ä¢ Fintype Œ≤
                                                   -- üéâ no goals
#align pgame.fintype_right PGame.fintypeRight

attribute [local instance] fintypeRight

instance fintypeRightMoves (x : PGame) [S : Short x] : Fintype x.RightMoves := by
  cases S; assumption
  -- ‚ä¢ Fintype (RightMoves (mk Œ±‚úù Œ≤‚úù L‚úù R‚úù))
           -- üéâ no goals
#align pgame.fintype_right_moves PGame.fintypeRightMoves

instance moveLeftShort (x : PGame) [S : Short x] (i : x.LeftMoves) : Short (x.moveLeft i) := by
  cases' S with _ _ _ _ L _ _ _; apply L
  -- ‚ä¢ Short (moveLeft (mk Œ±‚úù Œ≤‚úù L‚úù R‚úù) i)
                                 -- üéâ no goals
#align pgame.move_left_short PGame.moveLeftShort

/-- Extracting the `Short` instance for a move by Left.
This would be a dangerous instance potentially introducing new metavariables
in typeclass search, so we only make it an instance locally.
-/
def moveLeftShort' {xl xr} (xL xR) [S : Short (mk xl xr xL xR)] (i : xl) : Short (xL i) := by
  cases' S with _ _ _ _ L _ _ _; apply L
  -- ‚ä¢ Short (xL i)
                                 -- üéâ no goals
#align pgame.move_left_short' PGame.moveLeftShort'

attribute [local instance] moveLeftShort'

instance moveRightShort (x : PGame) [S : Short x] (j : x.RightMoves) : Short (x.moveRight j) := by
  cases' S with _ _ _ _ _ R _ _; apply R
  -- ‚ä¢ Short (moveRight (mk Œ±‚úù Œ≤‚úù L‚úù R‚úù) j)
                                 -- üéâ no goals
#align pgame.move_right_short PGame.moveRightShort

/-- Extracting the `Short` instance for a move by Right.
This would be a dangerous instance potentially introducing new metavariables
in typeclass search, so we only make it an instance locally.
-/
def moveRightShort' {xl xr} (xL xR) [S : Short (mk xl xr xL xR)] (j : xr) : Short (xR j) := by
  cases' S with _ _ _ _ _ R _ _; apply R
  -- ‚ä¢ Short (xR j)
                                 -- üéâ no goals
#align pgame.move_right_short' PGame.moveRightShort'

attribute [local instance] moveRightShort'

theorem short_birthday (x : PGame.{u}) : [Short x] ‚Üí x.birthday < Ordinal.omega := by
  -- Porting note: Again `induction` is used instead of `pgame_wf_tac`
  induction x with
  | mk xl xr xL xR ihl ihr =>
    intro hs
    rcases hs with ‚ü®sL, sR‚ü©
    rw [birthday, max_lt_iff]
    constructor
    all_goals
      rw [‚Üê Cardinal.ord_aleph0]
      refine'
        Cardinal.lsub_lt_ord_of_isRegular.{u, u} Cardinal.isRegular_aleph0
          (Cardinal.lt_aleph0_of_finite _) fun i => _
      rw [Cardinal.ord_aleph0]
    ¬∑ apply ihl
    ¬∑ apply ihr
#align pgame.short_birthday PGame.short_birthday

/-- This leads to infinite loops if made into an instance. -/
def Short.ofIsEmpty {l r xL xR} [IsEmpty l] [IsEmpty r] : Short (PGame.mk l r xL xR) :=
  Short.mk isEmptyElim isEmptyElim
#align pgame.short.of_is_empty PGame.Short.ofIsEmpty

instance short0 : Short 0 :=
  Short.ofIsEmpty
#align pgame.short_0 PGame.short0

instance short1 : Short 1 :=
  Short.mk (fun i => by cases i; infer_instance) fun j => by cases j
                        -- ‚ä¢ Short 0
                                 -- üéâ no goals
                                                             -- üéâ no goals
#align pgame.short_1 PGame.short1

/-- Evidence that every `PGame` in a list is `Short`. -/
class inductive ListShort : List PGame.{u} ‚Üí Type (u + 1)
  | nil : ListShort []
  -- Porting note: We introduce `cons` as a separate instance because attempting to use
  -- `[ListShort tl]` as a constructor argument errors saying that `ListShort tl` is not a class.
  -- Is this a bug in `class inductive`?
  | cons' {hd : PGame.{u}} {tl : List PGame.{u}} : Short hd ‚Üí ListShort tl ‚Üí ListShort (hd::tl)
#align pgame.list_short PGame.ListShort

attribute [instance] ListShort.nil

instance ListShort.cons (hd : PGame.{u}) [short_hd : Short hd]
                        (tl : List PGame.{u}) [short_tl : ListShort tl] :
    ListShort (hd::tl) :=
  cons' short_hd short_tl
#align pgame.list_short.cons PGame.ListShort.cons

-- Porting note: use `List.get` instead of `List.nthLe` because it has been deprecated
instance listShortGet :
    ‚àÄ (L : List PGame.{u}) [ListShort L] (i : Fin (List.length L)), Short (List.get L i)
  | [], _, n => by
    exfalso
    -- ‚ä¢ False
    rcases n with ‚ü®_, ‚ü®‚ü©‚ü©
    -- üéâ no goals
    -- Porting note: The proof errors unless `done` or a `;` is added after `rcases`
    done
    -- üéâ no goals
  | _::_, ListShort.cons' S _, ‚ü®0, _‚ü© => S
  | hd::tl, ListShort.cons' _ S, ‚ü®n + 1, h‚ü© =>
    @listShortGet tl S ‚ü®n, (add_lt_add_iff_right 1).mp h‚ü©

set_option linter.deprecated false in
@[deprecated listShortGet]
instance listShortNthLe (L : List PGame.{u}) [ListShort L] (i : Fin (List.length L)) :
    Short (List.nthLe L i i.is_lt) := by
  rw [List.nthLe_eq]
  -- ‚ä¢ Short (List.get L { val := ‚Üëi, isLt := (_ : ‚Üëi < List.length L) })
  apply listShortGet
  -- üéâ no goals
#align pgame.list_short_nth_le PGame.listShortNthLe

instance shortOfLists : ‚àÄ (L R : List PGame) [ListShort L] [ListShort R], Short (PGame.ofLists L R)
  | L, R, _, _ => by
    apply Short.mk
    -- ‚ä¢ (i : ULift (Fin (List.length L))) ‚Üí Short (List.nthLe L ‚Üëi.down (_ : ‚Üëi.down ‚Ä¶
    ¬∑ intros; infer_instance
      -- ‚ä¢ Short (List.nthLe L ‚Üëi‚úù.down (_ : ‚Üëi‚úù.down < List.length L))
              -- üéâ no goals
    ¬∑ intros; apply PGame.listShortGet
      -- ‚ä¢ Short (List.nthLe R ‚Üëj‚úù.down (_ : ‚Üëj‚úù.down < List.length R))
              -- üéâ no goals
#align pgame.short_of_lists PGame.shortOfLists

/-- If `x` is a short game, and `y` is a relabelling of `x`, then `y` is also short. -/
def shortOfRelabelling : ‚àÄ {x y : PGame.{u}}, Relabelling x y ‚Üí Short x ‚Üí Short y
  | x, y, ‚ü®L, R, rL, rR‚ü©, S => by
    haveI := Fintype.ofEquiv _ L
    -- ‚ä¢ Short y
    haveI := Fintype.ofEquiv _ R
    -- ‚ä¢ Short y
    exact
      Short.mk'
        (fun i => by rw [‚Üê L.right_inv i]; apply shortOfRelabelling (rL (L.symm i)) inferInstance)
        fun j => by simpa using shortOfRelabelling (rR (R.symm j)) inferInstance
#align pgame.short_of_relabelling PGame.shortOfRelabelling

instance shortNeg : ‚àÄ (x : PGame.{u}) [Short x], Short (-x)
  | mk xl xr xL xR, _ => by
    exact Short.mk (fun i => shortNeg _) fun i => shortNeg _
    -- üéâ no goals
-- Porting note: `decreasing_by pgame_wf_tac` is no longer needed.
#align pgame.short_neg PGame.shortNeg

instance shortAdd : ‚àÄ (x y : PGame.{u}) [Short x] [Short y], Short (x + y)
  | mk xl xr xL xR, mk yl yr yL yR, _, _ => by
    apply Short.mk;
    all_goals
      rintro ‚ü®i‚ü©
      ¬∑ apply shortAdd
      ¬∑ change Short (mk xl xr xL xR + _); apply shortAdd
-- Porting note: In Lean 3 `using_well_founded` didn't need this to be explicit.
termination_by shortAdd x y _ _ => Prod.mk x y
-- Porting note: `decreasing_by pgame_wf_tac` is no longer needed.
#align pgame.short_add PGame.shortAdd

instance shortNat : ‚àÄ n : ‚Ñï, Short n
  | 0 => PGame.short0
  | n + 1 => @PGame.shortAdd _ _ (shortNat n) PGame.short1
#align pgame.short_nat PGame.shortNat

instance shortOfNat (n : ‚Ñï) [Nat.AtLeastTwo n] : Short (no_index (OfNat.ofNat n)) := shortNat n

-- Porting note: `bit0` and `bit1` are deprecated so these instances can probably be removed.
set_option linter.deprecated false in
instance shortBit0 (x : PGame.{u}) [Short x] : Short (bit0 x) := by dsimp [bit0]; infer_instance
                                                                    -- ‚ä¢ Short (x + x)
                                                                                  -- üéâ no goals
#align pgame.short_bit0 PGame.shortBit0

set_option linter.deprecated false in
instance shortBit1 (x : PGame.{u}) [Short x] : Short (bit1 x) := by dsimp [bit1]; infer_instance
                                                                    -- ‚ä¢ Short (bit0 x + 1)
                                                                                  -- üéâ no goals
#align pgame.short_bit1 PGame.shortBit1

/-- Auxiliary construction of decidability instances.
We build `Decidable (x ‚â§ y)` and `Decidable (x ‚ßè y)` in a simultaneous induction.
Instances for the two projections separately are provided below.
-/
def leLfDecidable : ‚àÄ (x y : PGame.{u}) [Short x] [Short y], Decidable (x ‚â§ y) √ó Decidable (x ‚ßè y)
  | mk xl xr xL xR, mk yl yr yL yR, shortx, shorty => by
    constructor
    -- ‚ä¢ Decidable (mk xl xr xL xR ‚â§ mk yl yr yL yR)
    ¬∑ refine' @decidable_of_iff' _ _ mk_le_mk (id _)
      -- ‚ä¢ Decidable ((‚àÄ (i : xl), xL i ‚ßè mk yl yr yL yR) ‚àß ‚àÄ (j : yr), mk xl xr xL xR  ‚Ä¶
      apply @And.decidable _ _ ?_ ?_
      -- ‚ä¢ Decidable (‚àÄ (i : xl), xL i ‚ßè mk yl yr yL yR)
      ¬∑ apply @Fintype.decidableForallFintype xl _ ?_ _
        -- ‚ä¢ DecidablePred fun a => xL a ‚ßè mk yl yr yL yR
        intro i
        -- ‚ä¢ Decidable ((fun a => xL a ‚ßè mk yl yr yL yR) i)
        apply (leLfDecidable _ _).2
        -- üéâ no goals
      ¬∑ apply @Fintype.decidableForallFintype yr _ ?_ _
        -- ‚ä¢ DecidablePred fun a => mk xl xr xL xR ‚ßè yR a
        intro i
        -- ‚ä¢ Decidable ((fun a => mk xl xr xL xR ‚ßè yR a) i)
        apply (leLfDecidable _ _).2
        -- üéâ no goals
    ¬∑ refine' @decidable_of_iff' _ _ mk_lf_mk (id _)
      -- ‚ä¢ Decidable ((‚àÉ i, mk xl xr xL xR ‚â§ yL i) ‚à® ‚àÉ j, xR j ‚â§ mk yl yr yL yR)
      apply @Or.decidable _ _ ?_ ?_
      -- ‚ä¢ Decidable (‚àÉ i, mk xl xr xL xR ‚â§ yL i)
      ¬∑ apply @Fintype.decidableExistsFintype yl _ ?_ _
        -- ‚ä¢ DecidablePred fun a => mk xl xr xL xR ‚â§ yL a
        intro i
        -- ‚ä¢ Decidable ((fun a => mk xl xr xL xR ‚â§ yL a) i)
        apply (leLfDecidable _ _).1
        -- üéâ no goals
      ¬∑ apply @Fintype.decidableExistsFintype xr _ ?_ _
        -- ‚ä¢ DecidablePred fun a => xR a ‚â§ mk yl yr yL yR
        intro i
        -- ‚ä¢ Decidable ((fun a => xR a ‚â§ mk yl yr yL yR) i)
        apply (leLfDecidable _ _).1
        -- üéâ no goals
-- Porting note: In Lean 3 `using_well_founded` didn't need this to be explicit.
termination_by leLfDecidable x y _ _ => Prod.mk x y
-- Porting note: `decreasing_by pgame_wf_tac` is no longer needed.
#align pgame.le_lf_decidable PGame.leLfDecidable

instance leDecidable (x y : PGame.{u}) [Short x] [Short y] : Decidable (x ‚â§ y) :=
  (leLfDecidable x y).1
#align pgame.le_decidable PGame.leDecidable

instance lfDecidable (x y : PGame.{u}) [Short x] [Short y] : Decidable (x ‚ßè y) :=
  (leLfDecidable x y).2
#align pgame.lf_decidable PGame.lfDecidable

instance ltDecidable (x y : PGame.{u}) [Short x] [Short y] : Decidable (x < y) :=
  And.decidable
#align pgame.lt_decidable PGame.ltDecidable

instance equivDecidable (x y : PGame.{u}) [Short x] [Short y] : Decidable (x ‚âà y) :=
  And.decidable
#align pgame.equiv_decidable PGame.equivDecidable

example : Short 0 := by infer_instance
                        -- üéâ no goals

example : Short 1 := by infer_instance
                        -- üéâ no goals

example : Short 2 := by infer_instance
                        -- üéâ no goals

example : Short (-2) := by infer_instance
                           -- üéâ no goals

example : Short (ofLists [0] [1]) := by infer_instance
                                        -- üéâ no goals

example : Short (ofLists [-2, -1] [1]) := by infer_instance
                                             -- üéâ no goals

example : Short (0 + 0) := by infer_instance
                              -- üéâ no goals

example : Decidable ((1 : PGame) ‚â§ 1) := by infer_instance
                                            -- üéâ no goals

-- No longer works since definitional reduction of well-founded definitions has been restricted.
-- example : (0 : PGame.{u}) ‚â§ 0 := by decide
-- example : (1 : PGame.{u}) ‚â§ 1 := by decide
end PGame
