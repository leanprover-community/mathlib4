/-
Copyright (c) 2018 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro
-/
module

public import Mathlib.Data.Fin.VecNotation
public import Mathlib.Data.SetLike.Basic
public import Mathlib.Logic.Small.Basic
public import Mathlib.SetTheory.ZFC.PSet

/-!
# A model of ZFC

In this file, we model Zermelo-Fraenkel set theory (+ choice) using Lean's underlying type theory,
building on the pre-sets defined in `Mathlib/SetTheory/ZFC/PSet.lean`.

The theory of classes is developed in `Mathlib/SetTheory/ZFC/Class.lean`.

## Main definitions

* `ZFSet`: ZFC set. Defined as `PSet` quotiented by `PSet.Equiv`, the extensional equivalence.
* `ZFSet.choice`: Axiom of choice. Proved from Lean's axiom of choice.
* `ZFSet.omega`: The von Neumann ordinal `œâ` as a `Set`.
* `Classical.allZFSetDefinable`: All functions are classically definable.
* `ZFSet.IsFunc` : Predicate that a ZFC set is a subset of `x √ó y` that can be considered as a ZFC
  function `x ‚Üí y`. That is, each member of `x` is related by the ZFC set to exactly one member of
  `y`.
* `ZFSet.funs`: ZFC set of ZFC functions `x ‚Üí y`.
* `ZFSet.Hereditarily p x`: Predicate that every set in the transitive closure of `x` has property
  `p`.

## Notes

To avoid confusion between the Lean `Set` and the ZFC `Set`, docstrings in this file refer to them
respectively as "`Set`" and "ZFC set".
-/

@[expose] public section


universe u

/-- The ZFC universe of sets consists of the type of pre-sets,
  quotiented by extensional equivalence. -/
@[pp_with_univ]
def ZFSet : Type (u + 1) :=
  Quotient PSet.setoid.{u}

namespace ZFSet

/-- Turns a pre-set into a ZFC set. -/
def mk : PSet ‚Üí ZFSet :=
  Quotient.mk''

@[simp]
theorem mk_eq (x : PSet) : @Eq ZFSet ‚ü¶x‚üß (mk x) :=
  rfl

@[simp]
theorem mk_out : ‚àÄ x : ZFSet, mk x.out = x :=
  Quotient.out_eq

/-- A set function is "definable" if it is the image of some n-ary `PSet`
  function. This isn't exactly definability, but is useful as a sufficient
  condition for functions that have a computable image. -/
class Definable (n) (f : (Fin n ‚Üí ZFSet.{u}) ‚Üí ZFSet.{u}) where
  /-- Turns a definable function into an n-ary `PSet` function. -/
  out : (Fin n ‚Üí PSet.{u}) ‚Üí PSet.{u}
  /-- A set function `f` is the image of `Definable.out f`. -/
  mk_out xs : mk (out xs) = f (mk <| xs ¬∑) := by simp

attribute [simp] Definable.mk_out

/-- An abbrev of `ZFSet.Definable` for unary functions. -/
abbrev Definable‚ÇÅ (f : ZFSet.{u} ‚Üí ZFSet.{u}) := Definable 1 (fun s ‚Ü¶ f (s 0))

/-- A simpler constructor for `ZFSet.Definable‚ÇÅ`. -/
abbrev Definable‚ÇÅ.mk {f : ZFSet.{u} ‚Üí ZFSet.{u}}
    (out : PSet.{u} ‚Üí PSet.{u}) (mk_out : ‚àÄ x, ‚ü¶out x‚üß = f ‚ü¶x‚üß) :
    Definable‚ÇÅ f where
  out xs := out (xs 0)
  mk_out xs := mk_out (xs 0)

/-- Turns a unary definable function into a unary `PSet` function. -/
abbrev Definable‚ÇÅ.out (f : ZFSet.{u} ‚Üí ZFSet.{u}) [Definable‚ÇÅ f] :
    PSet.{u} ‚Üí PSet.{u} :=
  fun x ‚Ü¶ Definable.out (fun s ‚Ü¶ f (s 0)) ![x]

lemma Definable‚ÇÅ.mk_out {f : ZFSet.{u} ‚Üí ZFSet.{u}} [Definable‚ÇÅ f]
    {x : PSet} :
    .mk (out f x) = f (.mk x) :=
  Definable.mk_out ![x]

/-- An abbrev of `ZFSet.Definable` for binary functions. -/
abbrev Definable‚ÇÇ (f : ZFSet.{u} ‚Üí ZFSet.{u} ‚Üí ZFSet.{u}) := Definable 2 (fun s ‚Ü¶ f (s 0) (s 1))

/-- A simpler constructor for `ZFSet.Definable‚ÇÇ`. -/
abbrev Definable‚ÇÇ.mk {f : ZFSet.{u} ‚Üí ZFSet.{u} ‚Üí ZFSet.{u}}
    (out : PSet.{u} ‚Üí PSet.{u} ‚Üí PSet.{u}) (mk_out : ‚àÄ x y, ‚ü¶out x y‚üß = f ‚ü¶x‚üß ‚ü¶y‚üß) :
    Definable‚ÇÇ f where
  out xs := out (xs 0) (xs 1)
  mk_out xs := mk_out (xs 0) (xs 1)

/-- Turns a binary definable function into a binary `PSet` function. -/
abbrev Definable‚ÇÇ.out (f : ZFSet.{u} ‚Üí ZFSet.{u} ‚Üí ZFSet.{u}) [Definable‚ÇÇ f] :
    PSet.{u} ‚Üí PSet.{u} ‚Üí PSet.{u} :=
  fun x y ‚Ü¶ Definable.out (fun s ‚Ü¶ f (s 0) (s 1)) ![x, y]

lemma Definable‚ÇÇ.mk_out {f : ZFSet.{u} ‚Üí ZFSet.{u} ‚Üí ZFSet.{u}} [Definable‚ÇÇ f]
    {x y : PSet} :
    .mk (out f x y) = f (.mk x) (.mk y) :=
  Definable.mk_out ![x, y]

instance (f) [Definable‚ÇÅ f] (n g) [Definable n g] :
    Definable n (fun s ‚Ü¶ f (g s)) where
  out xs := Definable‚ÇÅ.out f (Definable.out g xs)

instance (f) [Definable‚ÇÇ f] (n g‚ÇÅ g‚ÇÇ) [Definable n g‚ÇÅ] [Definable n g‚ÇÇ] :
    Definable n (fun s ‚Ü¶ f (g‚ÇÅ s) (g‚ÇÇ s)) where
  out xs := Definable‚ÇÇ.out f (Definable.out g‚ÇÅ xs) (Definable.out g‚ÇÇ xs)

instance (n) (i) : Definable n (fun s ‚Ü¶ s i) where
  out s := s i

lemma Definable.out_equiv {n} (f : (Fin n ‚Üí ZFSet.{u}) ‚Üí ZFSet.{u}) [Definable n f]
    {xs ys : Fin n ‚Üí PSet} (h : ‚àÄ i, xs i ‚âà ys i) :
    out f xs ‚âà out f ys := by
  rw [‚Üê Quotient.eq_iff_equiv, mk_eq, mk_eq, mk_out, mk_out]
  exact congrArg _ (funext fun i ‚Ü¶ Quotient.sound (h i))

lemma Definable‚ÇÅ.out_equiv (f : ZFSet.{u} ‚Üí ZFSet.{u}) [Definable‚ÇÅ f]
    {x y : PSet} (h : x ‚âà y) :
    out f x ‚âà out f y :=
  Definable.out_equiv _ (by simp [h])

lemma Definable‚ÇÇ.out_equiv (f : ZFSet.{u} ‚Üí ZFSet.{u} ‚Üí ZFSet.{u}) [Definable‚ÇÇ f]
    {x‚ÇÅ y‚ÇÅ x‚ÇÇ y‚ÇÇ : PSet} (h‚ÇÅ : x‚ÇÅ ‚âà y‚ÇÅ) (h‚ÇÇ : x‚ÇÇ ‚âà y‚ÇÇ) :
    out f x‚ÇÅ x‚ÇÇ ‚âà out f y‚ÇÅ y‚ÇÇ :=
  Definable.out_equiv _ (by simp [Fin.forall_fin_succ, h‚ÇÅ, h‚ÇÇ])

end ZFSet

namespace Classical

open PSet ZFSet

/-- All functions are classically definable. -/
noncomputable def allZFSetDefinable {n} (F : (Fin n ‚Üí ZFSet.{u}) ‚Üí ZFSet.{u}) : Definable n F where
  out xs := (F (mk <| xs ¬∑)).out

end Classical

namespace ZFSet
variable {x y z : ZFSet.{u}}

open PSet

theorem eq {x y : PSet} : mk x = mk y ‚Üî Equiv x y :=
  Quotient.eq

theorem sound {x y : PSet} (h : PSet.Equiv x y) : mk x = mk y :=
  Quotient.sound h

theorem exact {x y : PSet} : mk x = mk y ‚Üí PSet.Equiv x y :=
  Quotient.exact

/-- The membership relation for ZFC sets is inherited from the membership relation for pre-sets. -/
protected def Mem : ZFSet ‚Üí ZFSet ‚Üí Prop :=
  Quotient.lift‚ÇÇ (¬∑ ‚àà ¬∑) fun _ _ _ _ hx hy =>
    propext ((Mem.congr_left hx).trans (Mem.congr_right hy))

instance : Membership ZFSet ZFSet where
  mem t s := ZFSet.Mem s t

@[simp]
theorem mk_mem_iff {x y : PSet} : mk x ‚àà mk y ‚Üî x ‚àà y :=
  Iff.rfl

@[ext] lemma ext : (‚àÄ z : ZFSet.{u}, z ‚àà x ‚Üî z ‚àà y) ‚Üí x = y :=
  Quotient.inductionOn‚ÇÇ x y fun _ _ h => Quotient.sound (Mem.ext fun w => h ‚ü¶w‚üß)

instance : SetLike ZFSet.{u} ZFSet.{u} where
  coe x := {y | y ‚àà x}
  coe_injective' x y hxy := by ext z; exact congr(z ‚àà $hxy)

instance : PartialOrder ZFSet.{u} := .ofSetLike ZFSet.{u} ZFSet.{u}

/-- Convert a ZFC set into a `Set` of ZFC sets -/
@[deprecated SetLike.coe (since := "2025-11-05")]
def toSet (u : ZFSet.{u}) : Set ZFSet.{u} :=
  { x | x ‚àà u }

@[deprecated SetLike.mem_coe (since := "2025-11-05")]
theorem mem_toSet (a u : ZFSet.{u}) : a ‚àà (u : Set ZFSet.{u}) ‚Üî a ‚àà u :=
  Iff.rfl

instance small_coe (x : ZFSet.{u}) : Small.{u} x :=
  Quotient.inductionOn x fun a => by
    let f (i : a.Type) : mk a := ‚ü®mk <| a.Func i, func_mem a i‚ü©
    suffices Function.Surjective f by exact small_of_surjective this
    rintro ‚ü®y, hb‚ü©
    induction y using Quotient.inductionOn
    obtain ‚ü®i, h‚ü© := hb
    exact ‚ü®i, Subtype.coe_injective (Quotient.sound h.symm)‚ü©

@[deprecated (since := "2025-11-05")] alias small_toSet := small_coe

/-- A nonempty set is one that contains some element. -/
protected def Nonempty (u : ZFSet.{u}) : Prop := (u : Set ZFSet.{u}).Nonempty

theorem nonempty_def (u : ZFSet) : u.Nonempty ‚Üî ‚àÉ x, x ‚àà u :=
  Iff.rfl

theorem nonempty_of_mem {x u : ZFSet} (h : x ‚àà u) : u.Nonempty :=
  ‚ü®x, h‚ü©

@[simp, norm_cast] lemma nonempty_coe : (x : Set ZFSet.{u}).Nonempty ‚Üî x.Nonempty := .rfl

@[deprecated (since := "2025-11-05")] alias nonempty_toSet_iff := nonempty_coe

/-- `x ‚äÜ y` as ZFC sets means that all members of `x` are members of `y`. -/
protected def Subset (x y : ZFSet.{u}) :=
  ‚àÄ ‚¶Éz‚¶Ñ, z ‚àà x ‚Üí z ‚àà y

instance : HasSubset ZFSet := ‚ü®ZFSet.Subset‚ü©
instance : HasSSubset ZFSet := ‚ü®(¬∑ < ¬∑)‚ü©

@[simp] lemma le_def : x ‚â§ y ‚Üî x ‚äÜ y := .rfl
@[simp] lemma lt_def : x < y ‚Üî x ‚äÇ y := .rfl

theorem subset_def {x y : ZFSet.{u}} : x ‚äÜ y ‚Üî ‚àÄ ‚¶Éz‚¶Ñ, z ‚àà x ‚Üí z ‚àà y :=
  Iff.rfl

instance : @Std.Refl ZFSet (¬∑ ‚äÜ ¬∑) :=
  ‚ü®fun _ _ => id‚ü©

instance : IsTrans ZFSet (¬∑ ‚äÜ ¬∑) :=
  ‚ü®fun _ _ _ hxy hyz _ ha => hyz (hxy ha)‚ü©

@[simp]
theorem subset_iff : ‚àÄ {x y : PSet}, mk x ‚äÜ mk y ‚Üî x ‚äÜ y
  | ‚ü®_, A‚ü©, ‚ü®_, _‚ü© =>
    ‚ü®fun h a => @h ‚ü¶A a‚üß (Mem.mk A a), fun h z =>
      Quotient.inductionOn z fun _ ‚ü®a, za‚ü© =>
        let ‚ü®b, ab‚ü© := h a
        ‚ü®b, za.trans ab‚ü©‚ü©

lemma coe_subset_coe : (x : Set ZFSet.{u}) ‚äÜ y ‚Üî x ‚äÜ y := by simp

@[deprecated (since := "2025-11-05")] alias toSet_subset_iff := coe_subset_coe

@[deprecated SetLike.coe_injective (since := "2025-11-05")]
theorem toSet_injective : Function.Injective ((‚Üë) : ZFSet.{u} ‚Üí Set ZFSet.{u}) :=
  SetLike.coe_injective

@[deprecated SetLike.coe_set_eq (since := "2025-11-05")]
lemma toSet_inj : (x : Set ZFSet.{u}) = y ‚Üî x = y := SetLike.coe_set_eq

instance : @Std.Antisymm ZFSet (¬∑ ‚äÜ ¬∑) :=
  ‚ü®@le_antisymm ZFSet _‚ü©

instance : IsNonstrictStrictOrder ZFSet (¬∑ ‚äÜ ¬∑) (¬∑ ‚äÇ ¬∑) :=
  ‚ü®fun _ _ ‚Ü¶ Iff.rfl‚ü©

/-- The empty ZFC set -/
protected def empty : ZFSet :=
  mk ‚àÖ

instance : EmptyCollection ZFSet :=
  ‚ü®ZFSet.empty‚ü©

instance : Inhabited ZFSet :=
  ‚ü®‚àÖ‚ü©

@[simp]
theorem notMem_empty (x) : x ‚àâ (‚àÖ : ZFSet.{u}) :=
  Quotient.inductionOn x PSet.notMem_empty

@[simp, norm_cast] lemma coe_empty : ((‚àÖ : ZFSet.{u}) : Set ZFSet.{u}) = ‚àÖ := by ext; simp

@[deprecated (since := "2025-11-05")] alias toSet_empty := coe_empty

@[simp]
theorem empty_subset (x : ZFSet.{u}) : (‚àÖ : ZFSet) ‚äÜ x :=
  Quotient.inductionOn x fun y => subset_iff.2 <| PSet.empty_subset y

@[simp]
theorem not_nonempty_empty : ¬¨ZFSet.Nonempty ‚àÖ := by simp [ZFSet.Nonempty]

@[simp]
theorem nonempty_mk_iff {x : PSet} : (mk x).Nonempty ‚Üî x.Nonempty := by
  refine ‚ü®?_, fun ‚ü®a, h‚ü© => ‚ü®mk a, h‚ü©‚ü©
  rintro ‚ü®a, h‚ü©
  induction a using Quotient.inductionOn
  exact ‚ü®_, h‚ü©

theorem eq_empty (x : ZFSet.{u}) : x = ‚àÖ ‚Üî ‚àÄ y : ZFSet.{u}, y ‚àâ x := by
  simp [ZFSet.ext_iff]

theorem eq_empty_or_nonempty (u : ZFSet) : u = ‚àÖ ‚à® u.Nonempty := by
  rw [eq_empty, ‚Üê not_exists]
  apply em'

/-- `Insert x y` is the set `{x} ‚à™ y` -/
protected def Insert : ZFSet ‚Üí ZFSet ‚Üí ZFSet :=
  Quotient.map‚ÇÇ PSet.insert
    fun _ _ uv ‚ü®_, _‚ü© ‚ü®_, _‚ü© ‚ü®Œ±Œ≤, Œ≤Œ±‚ü© =>
      ‚ü®fun o =>
        match o with
        | some a =>
          let ‚ü®b, hb‚ü© := Œ±Œ≤ a
          ‚ü®some b, hb‚ü©
        | none => ‚ü®none, uv‚ü©,
        fun o =>
        match o with
        | some b =>
          let ‚ü®a, ha‚ü© := Œ≤Œ± b
          ‚ü®some a, ha‚ü©
        | none => ‚ü®none, uv‚ü©‚ü©

instance : Insert ZFSet ZFSet :=
  ‚ü®ZFSet.Insert‚ü©

instance : Singleton ZFSet ZFSet :=
  ‚ü®fun x => insert x ‚àÖ‚ü©

instance : LawfulSingleton ZFSet ZFSet :=
  ‚ü®fun _ => rfl‚ü©

@[simp]
theorem mem_insert_iff {x y z : ZFSet.{u}} : x ‚àà insert y z ‚Üî x = y ‚à® x ‚àà z :=
  Quotient.inductionOn‚ÇÉ x y z fun _ _ _ => PSet.mem_insert_iff.trans (or_congr_left eq.symm)

theorem mem_insert (x y : ZFSet) : x ‚àà insert x y :=
  mem_insert_iff.2 <| Or.inl rfl

theorem mem_insert_of_mem {y z : ZFSet} (x) (h : z ‚àà y) : z ‚àà insert x y :=
  mem_insert_iff.2 <| Or.inr h

@[simp, norm_cast]
lemma coe_insert (x y : ZFSet) : ‚Üë(insert x y) = (insert x ‚Üëy : Set ZFSet) := by ext; simp

@[deprecated (since := "2025-11-05")] alias toSet_insert := coe_insert

@[simp]
theorem mem_singleton {x y : ZFSet.{u}} : x ‚àà ({y} : ZFSet.{u}) ‚Üî x = y :=
  Quotient.inductionOn‚ÇÇ x y fun _ _ => PSet.mem_singleton.trans eq.symm

theorem notMem_singleton {x y : ZFSet.{u}} : x ‚àâ ({y} : ZFSet.{u}) ‚Üî x ‚â† y :=
  mem_singleton.not

@[simp, norm_cast]
lemma coe_singleton (x : ZFSet) : (({x} : ZFSet) : Set ZFSet) = {x} := by ext; simp

@[deprecated (since := "2025-11-05")] alias toSet_singleton := coe_singleton

theorem insert_nonempty (u v : ZFSet) : (insert u v).Nonempty :=
  ‚ü®u, mem_insert u v‚ü©

theorem singleton_nonempty (u : ZFSet) : ZFSet.Nonempty {u} :=
  insert_nonempty u ‚àÖ

theorem mem_pair {x y z : ZFSet.{u}} : x ‚àà ({y, z} : ZFSet) ‚Üî x = y ‚à® x = z := by
  simp

@[simp]
theorem pair_eq_singleton (x : ZFSet) : {x, x} = ({x} : ZFSet) := by
  ext
  simp

@[simp]
theorem pair_eq_singleton_iff {x y z : ZFSet} : ({x, y} : ZFSet) = {z} ‚Üî x = z ‚àß y = z := by
  refine ‚ü®fun h ‚Ü¶ ?_, ?_‚ü©
  ¬∑ rw [‚Üê mem_singleton, ‚Üê mem_singleton]
    simp [‚Üê h]
  ¬∑ rintro ‚ü®rfl, rfl‚ü©
    exact pair_eq_singleton y

@[simp]
theorem singleton_eq_pair_iff {x y z : ZFSet} : ({x} : ZFSet) = {y, z} ‚Üî x = y ‚àß x = z := by
  rw [eq_comm, pair_eq_singleton_iff]
  simp_rw [eq_comm]

/-- `omega` is the first infinite von Neumann ordinal -/
def omega : ZFSet :=
  mk PSet.omega

@[simp]
theorem omega_zero : ‚àÖ ‚àà omega :=
  ‚ü®‚ü®0‚ü©, Equiv.rfl‚ü©

@[simp]
theorem omega_succ {n} : n ‚àà omega.{u} ‚Üí insert n n ‚àà omega.{u} :=
  Quotient.inductionOn n fun x ‚ü®‚ü®n‚ü©, h‚ü© =>
    ‚ü®‚ü®n + 1‚ü©,
      ZFSet.exact <|
        show insert (mk x) (mk x) = insert (mk <| ofNat n) (mk <| ofNat n) by
          rw [ZFSet.sound h]
          rfl‚ü©

/-- `{x ‚àà a | p x}` is the set of elements in `a` satisfying `p` -/
protected def sep (p : ZFSet ‚Üí Prop) : ZFSet ‚Üí ZFSet :=
  Quotient.map (PSet.sep fun y => p (mk y))
    fun ‚ü®Œ±, A‚ü© ‚ü®Œ≤, B‚ü© ‚ü®Œ±Œ≤, Œ≤Œ±‚ü© =>
      ‚ü®fun ‚ü®a, pa‚ü© =>
        let ‚ü®b, hb‚ü© := Œ±Œ≤ a
        ‚ü®‚ü®b, by simpa only [mk_func, ‚Üê ZFSet.sound hb]‚ü©, hb‚ü©,
        fun ‚ü®b, pb‚ü© =>
        let ‚ü®a, ha‚ü© := Œ≤Œ± b
        ‚ü®‚ü®a, by simpa only [mk_func, ZFSet.sound ha]‚ü©, ha‚ü©‚ü©

-- Porting note: the { x | p x } notation appears to be disabled in Lean 4.
instance : Sep ZFSet ZFSet :=
  ‚ü®ZFSet.sep‚ü©

@[simp]
theorem mem_sep {p : ZFSet.{u} ‚Üí Prop} {x y : ZFSet.{u}} :
    y ‚àà ZFSet.sep p x ‚Üî y ‚àà x ‚àß p y :=
  Quotient.inductionOn‚ÇÇ x y fun _ _ =>
    PSet.mem_sep (p := p ‚àò mk) fun _ _ h => (Quotient.sound h).subst

@[simp]
theorem sep_empty (p : ZFSet ‚Üí Prop) : (‚àÖ : ZFSet).sep p = ‚àÖ :=
  (eq_empty _).mpr fun _ h ‚Ü¶ notMem_empty _ (mem_sep.mp h).1

theorem sep_subset {x p} : ZFSet.sep p x ‚äÜ x :=
  fun _ h => (mem_sep.1 h).1

@[simp, norm_cast]
lemma coe_sep (a : ZFSet) (p : ZFSet ‚Üí Prop) : (ZFSet.sep p a : Set ZFSet) = {x ‚àà a | p x} := by
  ext
  simp

@[deprecated (since := "2025-11-05")] alias toSet_sep := coe_sep

/-- The powerset operation, the collection of subsets of a ZFC set -/
def powerset : ZFSet ‚Üí ZFSet :=
  Quotient.map PSet.powerset
    fun ‚ü®_, A‚ü© ‚ü®_, B‚ü© ‚ü®Œ±Œ≤, Œ≤Œ±‚ü© =>
      ‚ü®fun p =>
        ‚ü®{ b | ‚àÉ a, p a ‚àß Equiv (A a) (B b) }, fun ‚ü®a, pa‚ü© =>
          let ‚ü®b, ab‚ü© := Œ±Œ≤ a
          ‚ü®‚ü®b, a, pa, ab‚ü©, ab‚ü©,
          fun ‚ü®_, a, pa, ab‚ü© => ‚ü®‚ü®a, pa‚ü©, ab‚ü©‚ü©,
        fun q =>
        ‚ü®{ a | ‚àÉ b, q b ‚àß Equiv (A a) (B b) }, fun ‚ü®_, b, qb, ab‚ü© => ‚ü®‚ü®b, qb‚ü©, ab‚ü©, fun ‚ü®b, qb‚ü© =>
          let ‚ü®a, ab‚ü© := Œ≤Œ± b
          ‚ü®‚ü®a, b, qb, ab‚ü©, ab‚ü©‚ü©‚ü©

@[simp]
theorem mem_powerset {x y : ZFSet.{u}} : y ‚àà powerset x ‚Üî y ‚äÜ x :=
  Quotient.inductionOn‚ÇÇ x y fun _ _ => PSet.mem_powerset.trans subset_iff.symm

theorem sUnion_lem {Œ± Œ≤ : Type u} (A : Œ± ‚Üí PSet) (B : Œ≤ ‚Üí PSet) (Œ±Œ≤ : ‚àÄ a, ‚àÉ b, Equiv (A a) (B b)) :
    ‚àÄ a, ‚àÉ b, Equiv ((sUnion ‚ü®Œ±, A‚ü©).Func a) ((sUnion ‚ü®Œ≤, B‚ü©).Func b)
  | ‚ü®a, c‚ü© => by
    let ‚ü®b, hb‚ü© := Œ±Œ≤ a
    induction ea : A a with | _ Œ≥ Œì
    induction eb : B b with | _ Œ¥ Œî
    rw [ea, eb] at hb
    obtain ‚ü®Œ≥Œ¥, Œ¥Œ≥‚ü© := hb
    let c : (A a).Type := c
    let ‚ü®d, hd‚ü© := Œ≥Œ¥ (by rwa [ea] at c)
    use ‚ü®b, Eq.ndrec d (Eq.symm eb)‚ü©
    change PSet.Equiv ((A a).Func c) ((B b).Func (Eq.ndrec d eb.symm))
    match A a, B b, ea, eb, c, d, hd with
    | _, _, rfl, rfl, _, _, hd => exact hd

/-- The union operator, the collection of elements of elements of a ZFC set. Uses `‚ãÉ‚ÇÄ` notation,
scoped under the `ZFSet` namespace.
-/
def sUnion : ZFSet ‚Üí ZFSet :=
  Quotient.map PSet.sUnion
    fun ‚ü®_, A‚ü© ‚ü®_, B‚ü© ‚ü®Œ±Œ≤, Œ≤Œ±‚ü© =>
      ‚ü®sUnion_lem A B Œ±Œ≤, fun a =>
        Exists.elim
          (sUnion_lem B A (fun b => Exists.elim (Œ≤Œ± b) fun c hc => ‚ü®c, PSet.Equiv.symm hc‚ü©) a)
          fun b hb => ‚ü®b, PSet.Equiv.symm hb‚ü©‚ü©

@[inherit_doc]
scoped prefix:110 "‚ãÉ‚ÇÄ " => ZFSet.sUnion

/-- The intersection operator, the collection of elements in all of the elements of a ZFC set. We
define `‚ãÇ‚ÇÄ ‚àÖ = ‚àÖ`. Uses `‚ãÇ‚ÇÄ` notation, scoped under the `ZFSet` namespace. -/
def sInter (x : ZFSet) : ZFSet := (‚ãÉ‚ÇÄ x).sep (fun y => ‚àÄ z ‚àà x, y ‚àà z)

@[inherit_doc]
scoped prefix:110 "‚ãÇ‚ÇÄ " => ZFSet.sInter

@[simp]
theorem mem_sUnion {x y : ZFSet.{u}} : y ‚àà ‚ãÉ‚ÇÄ x ‚Üî ‚àÉ z ‚àà x, y ‚àà z :=
  Quotient.inductionOn‚ÇÇ x y fun _ _ => PSet.mem_sUnion.trans
    ‚ü®fun ‚ü®z, h‚ü© => ‚ü®‚ü¶z‚üß, h‚ü©, fun ‚ü®z, h‚ü© => Quotient.inductionOn z (fun z h => ‚ü®z, h‚ü©) h‚ü©

theorem mem_sInter {x y : ZFSet} (h : x.Nonempty) : y ‚àà ‚ãÇ‚ÇÄ x ‚Üî ‚àÄ z ‚àà x, y ‚àà z := by
  unfold sInter
  simp only [and_iff_right_iff_imp, mem_sep]
  intro mem
  apply mem_sUnion.mpr
  replace ‚ü®s, h‚ü© := h
  exact ‚ü®_, h, mem _ h‚ü©

@[simp]
theorem sUnion_empty : ‚ãÉ‚ÇÄ (‚àÖ : ZFSet.{u}) = ‚àÖ := by
  ext
  simp

@[simp]
theorem sInter_empty : ‚ãÇ‚ÇÄ (‚àÖ : ZFSet) = ‚àÖ := by simp [sInter]

theorem mem_of_mem_sInter {x y z : ZFSet} (hy : y ‚àà ‚ãÇ‚ÇÄ x) (hz : z ‚àà x) : y ‚àà z := by
  rcases eq_empty_or_nonempty x with (rfl | hx)
  ¬∑ exact (notMem_empty z hz).elim
  ¬∑ exact (mem_sInter hx).1 hy z hz

theorem mem_sUnion_of_mem {x y z : ZFSet} (hy : y ‚àà z) (hz : z ‚àà x) : y ‚àà ‚ãÉ‚ÇÄ x :=
  mem_sUnion.2 ‚ü®z, hz, hy‚ü©

theorem notMem_sInter_of_notMem {x y z : ZFSet} (hy : y ‚àâ z) (hz : z ‚àà x) : y ‚àâ ‚ãÇ‚ÇÄ x :=
  fun hx => hy <| mem_of_mem_sInter hx hz

@[simp]
theorem sUnion_singleton {x : ZFSet.{u}} : ‚ãÉ‚ÇÄ ({x} : ZFSet) = x :=
  ext fun y => by simp_rw [mem_sUnion, mem_singleton, exists_eq_left]

@[simp]
theorem sInter_singleton {x : ZFSet.{u}} : ‚ãÇ‚ÇÄ ({x} : ZFSet) = x :=
  ext fun y => by simp_rw [mem_sInter (singleton_nonempty x), mem_singleton, forall_eq]

@[simp, norm_cast]
lemma coe_sUnion (x : ZFSet.{u}) : (‚ãÉ‚ÇÄ x : Set ZFSet) = ‚ãÉ‚ÇÄ (SetLike.coe '' (x : Set ZFSet)) := by
  ext
  simp

@[deprecated (since := "2025-11-05")] alias toSet_sUnion := coe_sUnion

@[simp, norm_cast]
lemma coe_sInter (h : x.Nonempty) : (‚ãÇ‚ÇÄ x : Set ZFSet) = ‚ãÇ‚ÇÄ (SetLike.coe '' (x : Set ZFSet)) := by
  ext
  simp [mem_sInter h]

@[deprecated (since := "2025-11-05")] alias toSet_sInter := coe_sInter

theorem singleton_injective : Function.Injective (@singleton ZFSet ZFSet _) := fun x y H => by
  let this := congr_arg sUnion H
  rwa [sUnion_singleton, sUnion_singleton] at this

@[simp]
theorem singleton_inj {x y : ZFSet} : ({x} : ZFSet) = {y} ‚Üî x = y :=
  singleton_injective.eq_iff

/-- The binary union operation -/
protected def union (x y : ZFSet.{u}) : ZFSet.{u} :=
  ‚ãÉ‚ÇÄ {x, y}

/-- The binary intersection operation -/
protected def inter (x y : ZFSet.{u}) : ZFSet.{u} :=
  ZFSet.sep (fun z => z ‚àà y) x -- { z ‚àà x | z ‚àà y }

/-- The set difference operation -/
protected def diff (x y : ZFSet.{u}) : ZFSet.{u} :=
  ZFSet.sep (fun z => z ‚àâ y) x -- { z ‚àà x | z ‚àâ y }

instance : Union ZFSet :=
  ‚ü®ZFSet.union‚ü©

instance : Inter ZFSet :=
  ‚ü®ZFSet.inter‚ü©

instance : SDiff ZFSet :=
  ‚ü®ZFSet.diff‚ü©

@[simp] lemma sUnion_pair (x y : ZFSet.{u}) : ‚ãÉ‚ÇÄ ({x, y} : ZFSet.{u}) = x ‚à™ y := rfl

@[simp] lemma sep_mem (x y : ZFSet.{u}) : x.sep (¬∑ ‚àà y) = x ‚à© y := rfl
@[simp] lemma sep_notMem (x y : ZFSet.{u}) : x.sep (¬∑ ‚àâ y) = x \ y := rfl

@[simp] lemma mem_union : z ‚àà x ‚à™ y ‚Üî z ‚àà x ‚à® z ‚àà y := by simp [‚Üê sUnion_pair]
@[simp] lemma mem_inter : z ‚àà x ‚à© y ‚Üî z ‚àà x ‚àß z ‚àà y := by simp [‚Üê sep_mem]
@[simp] lemma mem_sdiff : z ‚àà x \ y ‚Üî z ‚àà x ‚àß z ‚àâ y := by simp [‚Üê sep_notMem]

@[deprecated (since := "2025-11-06")] alias mem_diff := mem_sdiff

@[simp, norm_cast]
lemma coe_union (x y : ZFSet.{u}) : ‚Üë(x ‚à™ y) = (‚Üëx ‚à™ ‚Üëy : Set ZFSet) := by ext; simp

@[deprecated (since := "2025-11-05")] alias toSet_union := coe_union

@[simp, norm_cast]
lemma coe_inter (x y : ZFSet.{u}) : ‚Üë(x ‚à© y) = (‚Üëx ‚à© ‚Üëy : Set ZFSet) := by ext; simp

@[deprecated (since := "2025-11-05")] alias toSet_inter := coe_inter

@[simp, norm_cast]
lemma coe_sdiff (x y : ZFSet.{u}) : ‚Üë(x \ y) = (‚Üëx \ ‚Üëy : Set ZFSet) := by ext; simp

@[deprecated (since := "2025-11-05")] alias toSet_sdiff := coe_sdiff

@[simp] lemma inter_eq_left_of_subset (hxy : x ‚äÜ y) : x ‚à© y = x := by ext; simpa using @hxy _
@[simp] lemma inter_eq_right_of_subset (hyx : y ‚äÜ x) : x ‚à© y = y := by ext; simpa using @hyx _

/-- `ZFSet.powerset` is equivalent to `Set.powerset`. -/
def powersetEquiv (x : ZFSet.{u}) : x.powerset ‚âÉ ùí´ (x : Set ZFSet) where
  toFun y := ‚ü®y.1, Set.mem_powerset (mem_powerset.1 y.2)‚ü©
  invFun s := ‚ü®x.sep (¬∑ ‚àà s.1), mem_powerset.2 sep_subset‚ü©
  left_inv := by simp +contextual [Function.LeftInverse]
  right_inv := by simp +contextual [Function.LeftInverse, Function.RightInverse, Set.setOf_and]

theorem insert_eq (x y : ZFSet) : insert x y = {x} ‚à™ y := by
  ext; simp

theorem mem_wf : @WellFounded ZFSet (¬∑ ‚àà ¬∑) :=
  (wellFounded_lift‚ÇÇ_iff (H := fun a b c d hx hy =>
    propext ((@Mem.congr_left a c hx).trans (@Mem.congr_right b d hy _)))).mpr PSet.mem_wf

/-- Induction on the `‚àà` relation. -/
@[elab_as_elim]
theorem inductionOn {p : ZFSet ‚Üí Prop} (x) (h : ‚àÄ x, (‚àÄ y ‚àà x, p y) ‚Üí p x) : p x :=
  mem_wf.induction x h

instance : IsWellFounded ZFSet (¬∑ ‚àà ¬∑) :=
  ‚ü®mem_wf‚ü©

instance : WellFoundedRelation ZFSet :=
  ‚ü®_, mem_wf‚ü©

theorem mem_asymm {x y : ZFSet} : x ‚àà y ‚Üí y ‚àâ x :=
  asymm_of (¬∑ ‚àà ¬∑)

theorem mem_irrefl (x : ZFSet) : x ‚àâ x :=
  irrefl_of (¬∑ ‚àà ¬∑) x

theorem not_subset_of_mem {x y : ZFSet} (h : x ‚àà y) : ¬¨ y ‚äÜ x :=
  fun h' ‚Ü¶ mem_irrefl _ (h' h)

theorem notMem_of_subset {x y : ZFSet} (h : x ‚äÜ y) : y ‚àâ x :=
  imp_not_comm.2 not_subset_of_mem h

theorem regularity (x : ZFSet.{u}) (h : x ‚â† ‚àÖ) : ‚àÉ y ‚àà x, x ‚à© y = ‚àÖ :=
  by_contradiction fun ne =>
    h <| (eq_empty x).2 fun y =>
      @inductionOn (fun z => z ‚àâ x) y fun z IH zx =>
        ne ‚ü®z, zx, (eq_empty _).2 fun w wxz =>
          let ‚ü®wx, wz‚ü© := mem_inter.1 wxz
          IH w wz wx‚ü©

/-- The image of a (definable) ZFC set function -/
def image (f : ZFSet ‚Üí ZFSet) [Definable‚ÇÅ f] : ZFSet ‚Üí ZFSet :=
  let r := Definable‚ÇÅ.out f
  Quotient.map (PSet.image r)
    fun _ _ e =>
      Mem.ext fun _ =>
        (mem_image (fun _ _ ‚Ü¶ Definable‚ÇÅ.out_equiv _)).trans <|
          Iff.trans
              ‚ü®fun ‚ü®w, h1, h2‚ü© => ‚ü®w, (Mem.congr_right e).1 h1, h2‚ü©, fun ‚ü®w, h1, h2‚ü© =>
                ‚ü®w, (Mem.congr_right e).2 h1, h2‚ü©‚ü© <|
            (mem_image (fun _ _ ‚Ü¶ Definable‚ÇÅ.out_equiv _)).symm

theorem image.mk (f : ZFSet.{u} ‚Üí ZFSet.{u}) [Definable‚ÇÅ f] (x) {y} : y ‚àà x ‚Üí f y ‚àà image f x :=
  Quotient.inductionOn‚ÇÇ x y fun ‚ü®_, _‚ü© _ ‚ü®a, ya‚ü© => by
    simp only [mk_eq, ‚Üê Definable‚ÇÅ.mk_out (f := f)]
    exact ‚ü®a, Definable‚ÇÅ.out_equiv f ya‚ü©

@[simp]
theorem mem_image {f : ZFSet.{u} ‚Üí ZFSet.{u}} [Definable‚ÇÅ f] {x y : ZFSet.{u}} :
    y ‚àà image f x ‚Üî ‚àÉ z ‚àà x, f z = y :=
  Quotient.inductionOn‚ÇÇ x y fun ‚ü®_, A‚ü© _ =>
    ‚ü®fun ‚ü®a, ya‚ü© => ‚ü®‚ü¶A a‚üß, Mem.mk A a, ((Quotient.sound ya).trans Definable‚ÇÅ.mk_out).symm‚ü©,
      fun ‚ü®_, hz, e‚ü© => e ‚ñ∏ image.mk _ _ hz‚ü©

@[simp, norm_cast]
lemma coe_image (f : ZFSet ‚Üí ZFSet) [Definable‚ÇÅ f] (x : ZFSet) :
    (image f x : Set ZFSet) = f '' x := by ext; simp

@[deprecated (since := "2025-11-05")] alias toSet_image := coe_image

section Small

variable {Œ± : Type*} [Small.{u} Œ±]

/-- The range of a type-indexed family of sets. -/
noncomputable def range (f : Œ± ‚Üí ZFSet.{u}) : ZFSet.{u} :=
  ‚ü¶‚ü®_, Quotient.out ‚àò f ‚àò (equivShrink Œ±).symm‚ü©‚üß

@[simp]
theorem mem_range {f : Œ± ‚Üí ZFSet.{u}} {x : ZFSet.{u}} : x ‚àà range f ‚Üî ‚àÉ i, f i = x :=
  Quotient.inductionOn x fun y => by
    constructor
    ¬∑ rintro ‚ü®z, hz‚ü©
      exact ‚ü®(equivShrink Œ±).symm z, Quotient.eq_mk_iff_out.2 hz.symm‚ü©
    ¬∑ rintro ‚ü®z, hz‚ü©
      use equivShrink Œ± z
      simpa [hz] using PSet.Equiv.symm (Quotient.mk_out y)

@[simp, norm_cast]
lemma coe_range (f : Œ± ‚Üí ZFSet.{u}) : (range f : Set ZFSet) = .range f := by ext; simp

@[deprecated (since := "2025-11-05")] alias toSet_range := coe_range

theorem mem_range_self {f : Œ± ‚Üí ZFSet.{u}} (a : Œ±) : f a ‚àà range f := by simp

/-- Indexed union of a family of ZFC sets. Uses `‚ãÉ` notation, scoped under the `ZFSet` namespace. -/
noncomputable def iUnion (f : Œ± ‚Üí ZFSet.{u}) : ZFSet.{u} :=
  sUnion (range f)

@[inherit_doc iUnion] scoped notation3 "‚ãÉ " (...)", " r:60:(scoped f => iUnion f) => r

@[simp]
theorem mem_iUnion {f : Œ± ‚Üí ZFSet.{u}} {x : ZFSet.{u}} : x ‚àà ‚ãÉ i, f i ‚Üî ‚àÉ i, x ‚àà f i := by
  simp [iUnion]

@[simp, norm_cast]
lemma coe_iUnion (f : Œ± ‚Üí ZFSet.{u}) : ‚Üë(‚ãÉ i, f i) = ‚ãÉ i, (f i : Set ZFSet) := by
  ext
  simp

@[deprecated (since := "2025-11-05")] alias toSet_iUnion := coe_iUnion

theorem subset_iUnion (f : Œ± ‚Üí ZFSet.{u}) (i : Œ±) : f i ‚äÜ ‚ãÉ i, f i := by
  intro x hx
  simpa using ‚ü®i, hx‚ü©

end Small

/-- Kuratowski ordered pair -/
def pair (x y : ZFSet.{u}) : ZFSet.{u} :=
  {{x}, {x, y}}

@[simp, norm_cast]
lemma coe_pair (x y : ZFSet.{u}) : (pair x y : Set ZFSet) = {{x}, {x, y}} := by simp [pair]

@[deprecated (since := "2025-11-05")] alias toSet_pair := coe_pair

/-- A subset of pairs `{(a, b) ‚àà x √ó y | p a b}` -/
def pairSep (p : ZFSet.{u} ‚Üí ZFSet.{u} ‚Üí Prop) (x y : ZFSet.{u}) : ZFSet.{u} :=
  (powerset (powerset (x ‚à™ y))).sep fun z => ‚àÉ a ‚àà x, ‚àÉ b ‚àà y, z = pair a b ‚àß p a b

@[simp]
theorem mem_pairSep {p} {x y z : ZFSet.{u}} :
    z ‚àà pairSep p x y ‚Üî ‚àÉ a ‚àà x, ‚àÉ b ‚àà y, z = pair a b ‚àß p a b := by
  refine mem_sep.trans ‚ü®And.right, fun e => ‚ü®?_, e‚ü©‚ü©
  grind [mem_pair, mem_powerset, mem_singleton, mem_union, pair, subset_def]

theorem pair_injective : Function.Injective2 pair := by
  intro x x' y y' H
  simp_rw [ZFSet.ext_iff, pair, mem_pair] at H
  obtain rfl : x = x' := And.left <| by simpa [or_and_left] using (H {x}).1 (Or.inl rfl)
  have he : y = x ‚Üí y = y' := by
    rintro rfl
    simpa [eq_comm] using H {y, y'}
  have hx := H {x, y}
  simp_rw [pair_eq_singleton_iff, true_and, or_true, true_iff] at hx
  refine ‚ü®rfl, hx.elim he fun hy ‚Ü¶ Or.elim ?_ he id‚ü©
  simpa using ZFSet.ext_iff.1 hy y

@[simp]
theorem pair_inj {x y x' y' : ZFSet} : pair x y = pair x' y' ‚Üî x = x' ‚àß y = y' :=
  pair_injective.eq_iff

/-- The Cartesian product, `{(a, b) | a ‚àà x, b ‚àà y}` -/
def prod : ZFSet.{u} ‚Üí ZFSet.{u} ‚Üí ZFSet.{u} :=
  pairSep fun _ _ => True

@[simp]
theorem mem_prod {x y z : ZFSet.{u}} : z ‚àà prod x y ‚Üî ‚àÉ a ‚àà x, ‚àÉ b ‚àà y, z = pair a b := by
  simp [prod]

theorem pair_mem_prod {x y a b : ZFSet.{u}} : pair a b ‚àà prod x y ‚Üî a ‚àà x ‚àß b ‚àà y := by
  simp

/-- `isFunc x y f` is the assertion that `f` is a subset of `x √ó y` which relates to each element
of `x` a unique element of `y`, so that we can consider `f` as a ZFC function `x ‚Üí y`. -/
def IsFunc (x y f : ZFSet.{u}) : Prop :=
  f ‚äÜ prod x y ‚àß ‚àÄ z : ZFSet.{u}, z ‚àà x ‚Üí ‚àÉ! w, pair z w ‚àà f

/-- `funs x y` is `y ^ x`, the set of all set functions `x ‚Üí y` -/
def funs (x y : ZFSet.{u}) : ZFSet.{u} :=
  ZFSet.sep (IsFunc x y) (powerset (prod x y))

@[simp]
theorem mem_funs {x y f : ZFSet.{u}} : f ‚àà funs x y ‚Üî IsFunc x y f := by simp [funs, IsFunc]

instance : Definable‚ÇÅ ({¬∑}) := .mk ({¬∑}) (fun _ ‚Ü¶ rfl)
instance : Definable‚ÇÇ insert := .mk insert (fun _ _ ‚Ü¶ rfl)
instance : Definable‚ÇÇ pair := by unfold pair; infer_instance

/-- Graph of a function: `map f x` is the ZFC function which maps `a ‚àà x` to `f a` -/
def map (f : ZFSet ‚Üí ZFSet) [Definable‚ÇÅ f] : ZFSet ‚Üí ZFSet :=
  image fun y => pair y (f y)

@[simp]
theorem mem_map {f : ZFSet ‚Üí ZFSet} [Definable‚ÇÅ f] {x y : ZFSet} :
    y ‚àà map f x ‚Üî ‚àÉ z ‚àà x, pair z (f z) = y :=
  mem_image

theorem map_unique {f : ZFSet.{u} ‚Üí ZFSet.{u}} [Definable‚ÇÅ f] {x z : ZFSet.{u}}
    (zx : z ‚àà x) : ‚àÉ! w, pair z w ‚àà map f x :=
  ‚ü®f z, image.mk _ _ zx, fun y yx => by
    let ‚ü®w, _, we‚ü© := mem_image.1 yx
    let ‚ü®wz, fy‚ü© := pair_injective we
    rw [‚Üê fy, wz]‚ü©

@[simp]
theorem map_isFunc {f : ZFSet ‚Üí ZFSet} [Definable‚ÇÅ f] {x y : ZFSet} :
    IsFunc x y (map f x) ‚Üî ‚àÄ z ‚àà x, f z ‚àà y :=
  ‚ü®fun ‚ü®ss, h‚ü© z zx =>
    let ‚ü®_, t1, t2‚ü© := h z zx
    (t2 (f z) (image.mk _ _ zx)).symm ‚ñ∏ (pair_mem_prod.1 (ss t1)).right,
    fun h =>
    ‚ü®fun _ yx =>
      let ‚ü®z, zx, ze‚ü© := mem_image.1 yx
      ze ‚ñ∏ pair_mem_prod.2 ‚ü®zx, h z zx‚ü©,
      fun _ => map_unique‚ü©‚ü©

/-- Given a predicate `p` on ZFC sets. `Hereditarily p x` means that `x` has property `p` and the
members of `x` are all `Hereditarily p`. -/
def Hereditarily (p : ZFSet ‚Üí Prop) (x : ZFSet) : Prop :=
  p x ‚àß ‚àÄ y ‚àà x, Hereditarily p y
termination_by x

section Hereditarily

variable {p : ZFSet.{u} ‚Üí Prop} {x y : ZFSet.{u}}

theorem hereditarily_iff : Hereditarily p x ‚Üî p x ‚àß ‚àÄ y ‚àà x, Hereditarily p y := by
  rw [‚Üê Hereditarily]

alias ‚ü®Hereditarily.def, _‚ü© := hereditarily_iff

theorem Hereditarily.self (h : x.Hereditarily p) : p x :=
  h.def.1

theorem Hereditarily.mem (h : x.Hereditarily p) (hy : y ‚àà x) : y.Hereditarily p :=
  h.def.2 _ hy

theorem Hereditarily.empty : Hereditarily p x ‚Üí p ‚àÖ := by
  apply @ZFSet.inductionOn _ x
  intro y IH h
  rcases ZFSet.eq_empty_or_nonempty y with (rfl | ‚ü®a, ha‚ü©)
  ¬∑ exact h.self
  ¬∑ exact IH a ha (h.mem ha)

end Hereditarily

end ZFSet
