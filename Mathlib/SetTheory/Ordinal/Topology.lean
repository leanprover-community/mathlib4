/-
Copyright (c) 2022 Violeta Hern√°ndez Palacios. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Violeta Hern√°ndez Palacios
-/
import Mathlib.SetTheory.Ordinal.Arithmetic
import Mathlib.Tactic.TFAE
import Mathlib.Topology.Order.Monotone

#align_import set_theory.ordinal.topology from "leanprover-community/mathlib"@"740acc0e6f9adf4423f92a485d0456fc271482da"

/-!
### Topology of ordinals

We prove some miscellaneous results involving the order topology of ordinals.

### Main results

* `Ordinal.isClosed_iff_sup` / `Ordinal.isClosed_iff_bsup`: A set of ordinals is closed iff it's
  closed under suprema.
* `Ordinal.isNormal_iff_strictMono_and_continuous`: A characterization of normal ordinal
  functions.
* `Ordinal.enumOrd_isNormal_iff_isClosed`: The function enumerating the ordinals of a set is
  normal iff the set is closed.
-/


noncomputable section

universe u v

open Cardinal Order Topology

namespace Ordinal

variable {s : Set Ordinal.{u}} {a : Ordinal.{u}}

instance : TopologicalSpace Ordinal.{u} := Preorder.topology Ordinal.{u}
instance : OrderTopology Ordinal.{u} := ‚ü®rfl‚ü©

theorem isOpen_singleton_iff : IsOpen ({a} : Set Ordinal) ‚Üî ¬¨IsLimit a := by
  refine ‚ü®fun h ‚ü®h‚ÇÄ, hsucc‚ü© => ?_, fun ha => ?_‚ü©
  ¬∑ obtain ‚ü®b, c, hbc, hbc'‚ü© :=
      (mem_nhds_iff_exists_Ioo_subset' ‚ü®0, Ordinal.pos_iff_ne_zero.2 h‚ÇÄ‚ü© ‚ü®_, lt_succ a‚ü©).1
        (h.mem_nhds rfl)
    have hba := hsucc b hbc.1
    exact hba.ne (hbc' ‚ü®lt_succ b, hba.trans hbc.2‚ü©)
  ¬∑ rcases zero_or_succ_or_limit a with (rfl | ‚ü®b, rfl‚ü© | ha')
    ¬∑ rw [‚Üê bot_eq_zero, ‚Üê Set.Iic_bot, ‚Üê Iio_succ]
      exact isOpen_Iio
    ¬∑ rw [‚Üê Set.Icc_self, Icc_succ_left, ‚Üê Ioo_succ_right]
      exact isOpen_Ioo
    ¬∑ exact (ha ha').elim
#align ordinal.is_open_singleton_iff Ordinal.isOpen_singleton_iff

-- Porting note (#11215): TODO: generalize to a `SuccOrder`
theorem nhds_right' (a : Ordinal) : ùìù[>] a = ‚ä• := (covBy_succ a).nhdsWithin_Ioi

-- todo: generalize to a `SuccOrder`
theorem nhds_left'_eq_nhds_ne (a : Ordinal) : ùìù[<] a = ùìù[‚â†] a := by
  rw [‚Üê nhds_left'_sup_nhds_right', nhds_right', sup_bot_eq]

-- todo: generalize to a `SuccOrder`
theorem nhds_left_eq_nhds (a : Ordinal) : ùìù[‚â§] a = ùìù a := by
  rw [‚Üê nhds_left_sup_nhds_right', nhds_right', sup_bot_eq]

-- todo: generalize to a `SuccOrder`
theorem nhdsBasis_Ioc (h : a ‚â† 0) : (ùìù a).HasBasis (¬∑ < a) (Set.Ioc ¬∑ a) :=
  nhds_left_eq_nhds a ‚ñ∏ nhdsWithin_Iic_basis' ‚ü®0, h.bot_lt‚ü©

-- todo: generalize to a `SuccOrder`
theorem nhds_eq_pure : ùìù a = pure a ‚Üî ¬¨IsLimit a :=
  (isOpen_singleton_iff_nhds_eq_pure _).symm.trans isOpen_singleton_iff

-- todo: generalize `Ordinal.IsLimit` and this lemma to a `SuccOrder`
theorem isOpen_iff : IsOpen s ‚Üî ‚àÄ o ‚àà s, IsLimit o ‚Üí ‚àÉ a < o, Set.Ioo a o ‚äÜ s := by
  refine isOpen_iff_mem_nhds.trans <| forall‚ÇÇ_congr fun o ho => ?_
  by_cases ho' : IsLimit o
  ¬∑ simp only [(nhdsBasis_Ioc ho'.1).mem_iff, ho', true_implies]
    refine exists_congr fun a => and_congr_right fun ha => ?_
    simp only [‚Üê Set.Ioo_insert_right ha, Set.insert_subset_iff, ho, true_and]
  ¬∑ simp [nhds_eq_pure.2 ho', ho, ho']
#align ordinal.is_open_iff Ordinal.isOpen_iff

open List Set in
theorem mem_closure_tfae (a : Ordinal.{u}) (s : Set Ordinal) :
    TFAE [a ‚àà closure s,
      a ‚àà closure (s ‚à© Iic a),
      (s ‚à© Iic a).Nonempty ‚àß sSup (s ‚à© Iic a) = a,
      ‚àÉ t, t ‚äÜ s ‚àß t.Nonempty ‚àß BddAbove t ‚àß sSup t = a,
      ‚àÉ (o : Ordinal.{u}), o ‚â† 0 ‚àß ‚àÉ (f : ‚àÄ x < o, Ordinal),
        (‚àÄ x hx, f x hx ‚àà s) ‚àß bsup.{u, u} o f = a,
      ‚àÉ (Œπ : Type u), Nonempty Œπ ‚àß ‚àÉ f : Œπ ‚Üí Ordinal, (‚àÄ i, f i ‚àà s) ‚àß sup.{u, u} f = a] := by
  tfae_have 1 ‚Üí 2
  ¬∑ simp only [mem_closure_iff_nhdsWithin_neBot, inter_comm s, nhdsWithin_inter', nhds_left_eq_nhds]
    exact id
  tfae_have 2 ‚Üí 3
  ¬∑ intro h
    rcases (s ‚à© Iic a).eq_empty_or_nonempty with he | hne
    ¬∑ simp [he] at h
    ¬∑ refine ‚ü®hne, (isLUB_of_mem_closure ?_ h).csSup_eq hne‚ü©
      exact fun x hx => hx.2
  tfae_have 3 ‚Üí 4
  ¬∑ exact fun h => ‚ü®_, inter_subset_left, h.1, bddAbove_Iic.mono inter_subset_right, h.2‚ü©
  tfae_have 4 ‚Üí 5
  ¬∑ rintro ‚ü®t, hts, hne, hbdd, rfl‚ü©
    have hlub : IsLUB t (sSup t) := isLUB_csSup hne hbdd
    let ‚ü®y, hyt‚ü© := hne
    classical
      refine ‚ü®succ (sSup t), succ_ne_zero _, fun x _ => if x ‚àà t then x else y, fun x _ => ?_, ?_‚ü©
      ¬∑ simp only
        split_ifs with h <;> exact hts ‚Äπ_‚Ä∫
      ¬∑ refine le_antisymm (bsup_le fun x _ => ?_) (csSup_le hne fun x hx => ?_)
        ¬∑ split_ifs <;> exact hlub.1 ‚Äπ_‚Ä∫
        ¬∑ refine (if_pos hx).symm.trans_le (le_bsup _ _ <| (hlub.1 hx).trans_lt (lt_succ _))
  tfae_have 5 ‚Üí 6
  ¬∑ rintro ‚ü®o, h‚ÇÄ, f, hfs, rfl‚ü©
    exact ‚ü®_, out_nonempty_iff_ne_zero.2 h‚ÇÄ, familyOfBFamily o f, fun _ => hfs _ _, rfl‚ü©
  tfae_have 6 ‚Üí 1
  ¬∑ rintro ‚ü®Œπ, hne, f, hfs, rfl‚ü©
    rw [sup, iSup]
    exact closure_mono (range_subset_iff.2 hfs) <| csSup_mem_closure (range_nonempty f)
      (bddAbove_range.{u, u} f)
  tfae_finish

theorem mem_closure_iff_sup :
    a ‚àà closure s ‚Üî
      ‚àÉ (Œπ : Type u) (_ : Nonempty Œπ) (f : Œπ ‚Üí Ordinal), (‚àÄ i, f i ‚àà s) ‚àß sup.{u, u} f = a :=
  ((mem_closure_tfae a s).out 0 5).trans <| by simp only [exists_prop]
#align ordinal.mem_closure_iff_sup Ordinal.mem_closure_iff_sup

theorem mem_closed_iff_sup (hs : IsClosed s) :
    a ‚àà s ‚Üî ‚àÉ (Œπ : Type u) (_hŒπ : Nonempty Œπ) (f : Œπ ‚Üí Ordinal),
      (‚àÄ i, f i ‚àà s) ‚àß sup.{u, u} f = a := by
  rw [‚Üê mem_closure_iff_sup, hs.closure_eq]
#align ordinal.mem_closed_iff_sup Ordinal.mem_closed_iff_sup

theorem mem_closure_iff_bsup :
    a ‚àà closure s ‚Üî
      ‚àÉ (o : Ordinal) (_ho : o ‚â† 0) (f : ‚àÄ a < o, Ordinal),
        (‚àÄ i hi, f i hi ‚àà s) ‚àß bsup.{u, u} o f = a :=
  ((mem_closure_tfae a s).out 0 4).trans <| by simp only [exists_prop]
#align ordinal.mem_closure_iff_bsup Ordinal.mem_closure_iff_bsup

theorem mem_closed_iff_bsup (hs : IsClosed s) :
    a ‚àà s ‚Üî
      ‚àÉ (o : Ordinal) (_ho : o ‚â† 0) (f : ‚àÄ a < o, Ordinal),
        (‚àÄ i hi, f i hi ‚àà s) ‚àß bsup.{u, u} o f = a := by
  rw [‚Üê mem_closure_iff_bsup, hs.closure_eq]
#align ordinal.mem_closed_iff_bsup Ordinal.mem_closed_iff_bsup

theorem isClosed_iff_sup :
    IsClosed s ‚Üî
      ‚àÄ {Œπ : Type u}, Nonempty Œπ ‚Üí ‚àÄ f : Œπ ‚Üí Ordinal, (‚àÄ i, f i ‚àà s) ‚Üí sup.{u, u} f ‚àà s := by
  use fun hs Œπ hŒπ f hf => (mem_closed_iff_sup hs).2 ‚ü®Œπ, hŒπ, f, hf, rfl‚ü©
  rw [‚Üê closure_subset_iff_isClosed]
  intro h x hx
  rcases mem_closure_iff_sup.1 hx with ‚ü®Œπ, hŒπ, f, hf, rfl‚ü©
  exact h hŒπ f hf
#align ordinal.is_closed_iff_sup Ordinal.isClosed_iff_sup

theorem isClosed_iff_bsup :
    IsClosed s ‚Üî
      ‚àÄ {o : Ordinal}, o ‚â† 0 ‚Üí ‚àÄ f : ‚àÄ a < o, Ordinal,
        (‚àÄ i hi, f i hi ‚àà s) ‚Üí bsup.{u, u} o f ‚àà s := by
  rw [isClosed_iff_sup]
  refine ‚ü®fun H o ho f hf => H (out_nonempty_iff_ne_zero.2 ho) _ ?_, fun H Œπ hŒπ f hf => ?_‚ü©
  ¬∑ exact fun i => hf _ _
  ¬∑ rw [‚Üê bsup_eq_sup]
    apply H (type_ne_zero_iff_nonempty.2 hŒπ)
    exact fun i hi => hf _
#align ordinal.is_closed_iff_bsup Ordinal.isClosed_iff_bsup

theorem isLimit_of_mem_frontier (ha : a ‚àà frontier s) : IsLimit a := by
  simp only [frontier_eq_closure_inter_closure, Set.mem_inter_iff, mem_closure_iff] at ha
  by_contra h
  rw [‚Üê isOpen_singleton_iff] at h
  rcases ha.1 _ h rfl with ‚ü®b, hb, hb'‚ü©
  rcases ha.2 _ h rfl with ‚ü®c, hc, hc'‚ü©
  rw [Set.mem_singleton_iff] at *
  subst hb; subst hc
  exact hc' hb'
#align ordinal.is_limit_of_mem_frontier Ordinal.isLimit_of_mem_frontier

theorem isNormal_iff_strictMono_and_continuous (f : Ordinal.{u} ‚Üí Ordinal.{u}) :
    IsNormal f ‚Üî StrictMono f ‚àß Continuous f := by
  refine ‚ü®fun h => ‚ü®h.strictMono, ?_‚ü©, ?_‚ü©
  ¬∑ rw [continuous_def]
    intro s hs
    rw [isOpen_iff] at *
    intro o ho ho'
    rcases hs _ ho (h.isLimit ho') with ‚ü®a, ha, has‚ü©
    rw [‚Üê IsNormal.bsup_eq.{u, u} h ho', lt_bsup] at ha
    rcases ha with ‚ü®b, hb, hab‚ü©
    exact
      ‚ü®b, hb, fun c hc =>
        Set.mem_preimage.2 (has ‚ü®hab.trans (h.strictMono hc.1), h.strictMono hc.2‚ü©)‚ü©
  ¬∑ rw [isNormal_iff_strictMono_limit]
    rintro ‚ü®h, h'‚ü©
    refine ‚ü®h, fun o ho a h => ?_‚ü©
    suffices o ‚àà f ‚Åª¬π' Set.Iic a from Set.mem_preimage.1 this
    rw [mem_closed_iff_sup (IsClosed.preimage h' (@isClosed_Iic _ _ _ _ a))]
    exact
      ‚ü®_, out_nonempty_iff_ne_zero.2 ho.1, typein (¬∑ < ¬∑), fun i => h _ (typein_lt_self i),
        sup_typein_limit ho.2‚ü©
#align ordinal.is_normal_iff_strict_mono_and_continuous Ordinal.isNormal_iff_strictMono_and_continuous

theorem enumOrd_isNormal_iff_isClosed (hs : s.Unbounded (¬∑ < ¬∑)) :
    IsNormal (enumOrd s) ‚Üî IsClosed s := by
  have Hs := enumOrd_strictMono hs
  refine
    ‚ü®fun h => isClosed_iff_sup.2 fun {Œπ} hŒπ f hf => ?_, fun h =>
      (isNormal_iff_strictMono_limit _).2 ‚ü®Hs, fun a ha o H => ?_‚ü©‚ü©
  ¬∑ let g : Œπ ‚Üí Ordinal.{u} := fun i => (enumOrdOrderIso hs).symm ‚ü®_, hf i‚ü©
    suffices enumOrd s (sup.{u, u} g) = sup.{u, u} f by
      rw [‚Üê this]
      exact enumOrd_mem hs _
    rw [@IsNormal.sup.{u, u, u} _ h Œπ g hŒπ]
    congr
    ext x
    change ((enumOrdOrderIso hs) _).val = f x
    rw [OrderIso.apply_symm_apply]
  ¬∑ rw [isClosed_iff_bsup] at h
    suffices enumOrd s a ‚â§ bsup.{u, u} a fun b (_ : b < a) => enumOrd s b from
      this.trans (bsup_le H)
    cases' enumOrd_surjective hs _
        (h ha.1 (fun b _ => enumOrd s b) fun b _ => enumOrd_mem hs b) with
      b hb
    rw [‚Üê hb]
    apply Hs.monotone
    by_contra! hba
    apply (Hs (lt_succ b)).not_le
    rw [hb]
    exact le_bsup.{u, u} _ _ (ha.2 _ hba)
#align ordinal.enum_ord_is_normal_iff_is_closed Ordinal.enumOrd_isNormal_iff_isClosed

open Set Filter

/-- A positive ordinal is an accumulation point of a set of ordinals if there
are elements in the set arbitrarily close to the ordinal from below. -/
def IsAcc (o : Ordinal) (S : Set Ordinal) : Prop :=
  o ‚â† 0 ‚àß ‚àÄ p < o, ‚àÉ s ‚àà S, p < s ‚àß s < o

/-- A set of ordinals is closed in an ordinal if it contains all of
its accumulation points below the ordinal. -/
def IsClosed (S : Set Ordinal) (o : Ordinal) : Prop :=
  ‚àÄ p < o, IsAcc p S ‚Üí p ‚àà S

theorem IsAcc.subset {o : Ordinal} {S T : Set Ordinal} (h : S ‚äÜ T) (ho : o.IsAcc S) :
    o.IsAcc T := ‚ü®ho.1, fun p plto ‚Ü¶ (ho.2 p plto).casesOn fun s hs ‚Ü¶ ‚ü®s, h hs.1, hs.2‚ü©‚ü©

theorem IsAcc.isLimit {o : Ordinal} {U : Set Ordinal} (h : o.IsAcc U) : IsLimit o := by
  refine' isLimit_of_not_succ_of_ne_zero (fun ‚ü®x, hx‚ü© ‚Ü¶ _) h.1
  rcases h.2 x (lt_of_lt_of_le (lt_succ x) hx.symm.le) with ‚ü®p, hp‚ü©
  exact (hx.symm ‚ñ∏ (succ_le_iff.mpr hp.2.1)).not_lt hp.2.2

theorem IsAcc.inter_Ioo_nonempty {o : Ordinal} {U : Set Ordinal} (hU : o.IsAcc U)
    {p : Ordinal} (hp : p < o) : (U ‚à© Ioo p o).Nonempty := by
  rcases hU.2 p hp with ‚ü®q, hq‚ü©
  exact ‚ü®q, hq.1, hq.2.1, hq.2.2‚ü©

theorem isClosed_zero (S : Set Ordinal) : IsClosed S 0 := fun _ h ‚Ü¶
  False.elim <| (Ordinal.zero_le _).not_lt h

theorem isAcc_of_accPt {o p : Ordinal} (A : Set Ordinal) (hpos : p ‚â† 0) (hp : p < o)
    (h : AccPt ‚ü®p, hp‚ü© (ùìü {x : Iio o | x.1 ‚àà A})) : IsAcc p A := by
  use hpos
  rw [accPt_iff_nhds] at h
  intro q qltp
  by_cases h' : Order.succ p = o
  ¬∑ let I : Set (Iio o) := Ioi ‚ü®q, qltp.trans hp‚ü©
    have Inh : I ‚àà nhds ‚ü®p, hp‚ü© := IsOpen.mem_nhds isOpen_Ioi qltp
    rcases h I Inh with ‚ü®r, ‚ü®rmemI, rmemA‚ü©, rnep‚ü©
    use r, rmemA, rmemI
    have : r ‚â§ p := Order.lt_succ_iff.mp (h' ‚ñ∏ r.2)
    exact this.lt_or_eq.resolve_right <| Subtype.coe_ne_coe.mpr rnep
  ¬∑ let I : Set (Iio o) := Ioo ‚ü®q, qltp.trans hp‚ü© ‚ü®p + 1,
      (succ_le_of_lt hp).lt_or_eq.resolve_right h'‚ü©
    have Inh : I ‚àà nhds ‚ü®p, hp‚ü© := IsOpen.mem_nhds isOpen_Ioo ‚ü®qltp, lt_add_one p‚ü©
    rcases h I Inh with ‚ü®r, ‚ü®rmemI, rmemA‚ü©, rnep‚ü©
    use r, rmemA, rmemI.1
    have : r.1 ‚â§ p := lt_succ_iff.mp rmemI.2
    exact this.lt_or_eq.resolve_right <| Subtype.coe_ne_coe.mpr rnep

theorem accPt_of_isAcc {o p : Ordinal} (A : Set Ordinal) (hp : p < o)
    (h : IsAcc p A) : AccPt ‚ü®p, hp‚ü© (ùìü {x : Iio o | x.1 ‚àà A}) := by
  rw [accPt_iff_nhds]
  intro u hu
  have lb := exists_Ioc_subset_of_mem_nhds hu ‚ü®‚ü®0, pos_of_gt' hp‚ü©,
    (Ordinal.zero_le p).lt_or_eq.resolve_right h.1.symm‚ü©
  rcases lb with ‚ü®a, pmem, hi‚ü©
  rcases h.2 a pmem with ‚ü®x, hx‚ü©
  use ‚ü®x, hx.2.2.trans hp‚ü©
  exact ‚ü®‚ü®hi ‚ü®hx.2.1, hx.2.2.le‚ü©, hx.1‚ü©, fun h ‚Ü¶ hx.2.2.ne <| Subtype.mk_eq_mk.mp h‚ü©

theorem isAcc_iff_accPt {o p : Ordinal} (A : Set Ordinal) (hp : p < o) (hpos : p ‚â† 0) :
    IsAcc p A ‚Üî AccPt ‚ü®p, hp‚ü© (ùìü {x : Iio o | x.1 ‚àà A}) :=
  ‚ü®accPt_of_isAcc A hp, isAcc_of_accPt A hpos hp‚ü©

theorem not_accPt_zero {o : Ordinal} (ho : o ‚â† 0) (S : Set (Iio o)) :
    ¬¨(AccPt ‚ü®0, Ordinal.pos_iff_ne_zero.mpr ho‚ü© (ùìü S)) := by
  intro h
  rw [accPt_iff_nhds] at h
  by_cases h' : 1 < o
  let I : Set (Iio o) := Iio ‚ü®1, h'‚ü©
  have : I ‚àà nhds ‚ü®0, Ordinal.pos_iff_ne_zero.mpr ho‚ü© :=
    IsOpen.mem_nhds isOpen_Iio (Ordinal.lt_one_iff_zero.mpr rfl)
  rcases h I this with ‚ü®x, ‚ü®hx, _‚ü©, xnz‚ü©
  exact xnz (SetCoe.ext (lt_one_iff_zero.mp hx))
  ¬∑ have := (not_lt_iff_eq_or_lt.mp h').resolve_right fun h ‚Ü¶
      ho <| Ordinal.lt_one_iff_zero.mp h
    rcases h Set.univ univ_mem with ‚ü®x, _, hx‚ü©
    exact hx <| SetCoe.ext <| Ordinal.lt_one_iff_zero.mp (this ‚ñ∏ x.2)

theorem isClosed_iff {o : Ordinal} (A : Set Ordinal) :
    IsClosed A o ‚Üî _root_.IsClosed {x : Iio o | x.1 ‚àà A} := by
  by_cases ho : o = 0
  ¬∑ rw [ho, eq_true (isClosed_zero A)]
    have : {x : Iio o | x.1 ‚àà A} = ‚àÖ := by
      apply eq_empty_iff_forall_not_mem.mpr
      intro x
      have := x.2
      simp_rw [ho] at this
      cases (Ordinal.zero_le _).not_lt this
    have : _root_.IsClosed {x : Iio o | x.1 ‚àà A} := by
      rw [this]
      exact isClosed_empty
    have := ho ‚ñ∏ (eq_true this)
    rw [this]
  rw [isClosed_iff_clusterPt]
  constructor
  ¬∑ intro h
    intro r hr
    change r.1 ‚àà A
    match clusterPt_principal hr with
    | .inl h' => exact h'
    | .inr h' =>
      have rnz : r.1 ‚â† 0 := fun h ‚Ü¶ by
        have := not_accPt_zero ho {x : Iio o | x.1 ‚àà A}
        simp_rw [‚Üê h] at this
        exact this h'
      have := (isAcc_iff_accPt A r.2 rnz)
      exact h r.1 r.2 (this.mpr h')
  ¬∑ intro h p plto hp
    apply h ‚ü®p, plto‚ü©
    have := (isAcc_iff_accPt A plto hp.1).mp hp
    apply ((acc_iff_cluster (‚ü®p, plto‚ü© : Iio o) _).mp this).mono inf_le_right

theorem IsClosed.sInter {o : Ordinal.{u}} {S : Set (Set Ordinal)} (h : ‚àÄ C ‚àà S, IsClosed C o) :
    IsClosed (‚ãÇ‚ÇÄ S) o :=
  fun p plto pAcc C CmemS ‚Ü¶ (h C CmemS) p plto (pAcc.subset (sInter_subset_of_mem CmemS))

theorem IsClosed.iInter {Œπ : Type u} {f : Œπ ‚Üí Set Ordinal} {o : Ordinal.{u}}
    (h : ‚àÄ i, IsClosed (f i) o) : IsClosed (‚ãÇ i, f i) o := by
  have := IsClosed.sInter (fun C (‚ü®i, fieqC‚ü© : C ‚àà range f) ‚Ü¶ fieqC ‚ñ∏ (h i))
  change IsClosed (‚ãÇ‚ÇÄ (range f)) o at this
  rwa [sInter_range] at this

end Ordinal
