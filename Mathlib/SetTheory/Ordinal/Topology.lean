/-
Copyright (c) 2022 Violeta Hern√°ndez Palacios. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Violeta Hern√°ndez Palacios
-/
module

public import Mathlib.SetTheory.Ordinal.Enum
public import Mathlib.Tactic.TFAE
public import Mathlib.Topology.Order.IsNormal
public import Mathlib.Topology.Order.Monotone
public import Mathlib.Topology.Order.SuccPred

/-!
### Topology of ordinals

We prove some miscellaneous results involving the order topology of ordinals.

### Main results

* `Ordinal.isClosed_iff_iSup` / `Ordinal.isClosed_iff_bsup`: A set of ordinals is closed iff it's
  closed under suprema.
* `Ordinal.isNormal_iff_strictMono_and_continuous`: A characterization of normal ordinal
  functions.
* `Ordinal.enumOrd_isNormal_iff_isClosed`: The function enumerating the ordinals of a set is
  normal iff the set is closed.
-/

@[expose] public section


noncomputable section

universe u v

open Cardinal Order Topology

namespace Ordinal

variable {s : Set Ordinal.{u}} {a : Ordinal.{u}}

instance : TopologicalSpace Ordinal.{u} := Preorder.topology Ordinal.{u}
instance : OrderTopology Ordinal.{u} := ‚ü®rfl‚ü©

@[deprecated SuccOrder.isOpen_singleton_iff (since := "2026-01-20")]
theorem isOpen_singleton_iff : IsOpen ({a} : Set Ordinal) ‚Üî ¬¨ IsSuccLimit a :=
  SuccOrder.isOpen_singleton_iff

@[deprecated SuccOrder.nhds_eq_pure (since := "2026-01-20")]
theorem nhds_eq_pure : ùìù a = pure a ‚Üî ¬¨ IsSuccLimit a :=
  SuccOrder.nhds_eq_pure

@[deprecated SuccOrder.isOpen_iff (since := "2026-01-20")]
theorem isOpen_iff : IsOpen s ‚Üî ‚àÄ o ‚àà s, IsSuccLimit o ‚Üí ‚àÉ a < o, Set.Ioo a o ‚äÜ s :=
  SuccOrder.isOpen_iff

open List Set in
theorem mem_closure_tfae (a : Ordinal.{u}) (s : Set Ordinal) :
    TFAE [a ‚àà closure s,
      a ‚àà closure (s ‚à© Iic a),
      (s ‚à© Iic a).Nonempty ‚àß sSup (s ‚à© Iic a) = a,
      ‚àÉ t, t ‚äÜ s ‚àß t.Nonempty ‚àß BddAbove t ‚àß sSup t = a,
      ‚àÉ (o : Ordinal.{u}), o ‚â† 0 ‚àß ‚àÉ (f : ‚àÄ x < o, Ordinal),
        (‚àÄ x hx, f x hx ‚àà s) ‚àß bsup.{u, u} o f = a,
      ‚àÉ (Œπ : Type u), Nonempty Œπ ‚àß ‚àÉ f : Œπ ‚Üí Ordinal, (‚àÄ i, f i ‚àà s) ‚àß ‚®Ü i, f i = a] := by
  tfae_have 1 ‚Üí 2 := by
    simpa only [mem_closure_iff_nhdsWithin_neBot, inter_comm s, nhdsWithin_inter',
      SuccOrder.nhdsLE_eq_nhds] using id
  tfae_have 2 ‚Üí 3
  | h => by
    rcases (s ‚à© Iic a).eq_empty_or_nonempty with he | hne
    ¬∑ simp [he] at h
    ¬∑ refine ‚ü®hne, (isLUB_of_mem_closure ?_ h).csSup_eq hne‚ü©
      exact fun x hx => hx.2
  tfae_have 3 ‚Üí 4
  | h => ‚ü®_, inter_subset_left, h.1, bddAbove_Iic.mono inter_subset_right, h.2‚ü©
  tfae_have 4 ‚Üí 5 := by
    rintro ‚ü®t, hts, hne, hbdd, rfl‚ü©
    have hlub : IsLUB t (sSup t) := isLUB_csSup hne hbdd
    let ‚ü®y, hyt‚ü© := hne
    classical
      refine ‚ü®succ (sSup t), succ_ne_zero _, fun x _ => if x ‚àà t then x else y, fun x _ => ?_, ?_‚ü©
      ¬∑ simp only
        split_ifs with h <;> exact hts ‚Äπ_‚Ä∫
      ¬∑ refine le_antisymm (bsup_le fun x _ => ?_) (csSup_le hne fun x hx => ?_)
        ¬∑ split_ifs <;> exact hlub.1 ‚Äπ_‚Ä∫
        ¬∑ refine (if_pos hx).symm.trans_le (le_bsup _ _ <| (hlub.1 hx).trans_lt (lt_succ _))
  tfae_have 5 ‚Üí 6 := by
    rintro ‚ü®o, h‚ÇÄ, f, hfs, rfl‚ü©
    exact ‚ü®_, toType_nonempty_iff_ne_zero.2 h‚ÇÄ, familyOfBFamily o f, fun _ => hfs _ _, rfl‚ü©
  tfae_have 6 ‚Üí 1 := by
    rintro ‚ü®Œπ, hne, f, hfs, rfl‚ü©
    exact closure_mono (range_subset_iff.2 hfs) <| csSup_mem_closure (range_nonempty f)
      (bddAbove_range.{u, u} f)
  tfae_finish

theorem mem_closure_iff_iSup :
    a ‚àà closure s ‚Üî
      ‚àÉ (Œπ : Type u) (_ : Nonempty Œπ) (f : Œπ ‚Üí Ordinal), (‚àÄ i, f i ‚àà s) ‚àß ‚®Ü i, f i = a := by
  apply ((mem_closure_tfae a s).out 0 5).trans
  simp_rw [exists_prop]

theorem mem_iff_iSup_of_isClosed (hs : IsClosed s) :
    a ‚àà s ‚Üî ‚àÉ (Œπ : Type u) (_hŒπ : Nonempty Œπ) (f : Œπ ‚Üí Ordinal),
      (‚àÄ i, f i ‚àà s) ‚àß ‚®Ü i, f i = a := by
  rw [‚Üê mem_closure_iff_iSup, hs.closure_eq]

theorem mem_closure_iff_bsup :
    a ‚àà closure s ‚Üî
      ‚àÉ (o : Ordinal) (_ho : o ‚â† 0) (f : ‚àÄ a < o, Ordinal),
        (‚àÄ i hi, f i hi ‚àà s) ‚àß bsup.{u, u} o f = a := by
  apply ((mem_closure_tfae a s).out 0 4).trans
  simp_rw [exists_prop]

theorem mem_closed_iff_bsup (hs : IsClosed s) :
    a ‚àà s ‚Üî
      ‚àÉ (o : Ordinal) (_ho : o ‚â† 0) (f : ‚àÄ a < o, Ordinal),
        (‚àÄ i hi, f i hi ‚àà s) ‚àß bsup.{u, u} o f = a := by
  rw [‚Üê mem_closure_iff_bsup, hs.closure_eq]

theorem isClosed_iff_iSup :
    IsClosed s ‚Üî
      ‚àÄ {Œπ : Type u}, Nonempty Œπ ‚Üí ‚àÄ f : Œπ ‚Üí Ordinal, (‚àÄ i, f i ‚àà s) ‚Üí ‚®Ü i, f i ‚àà s := by
  use fun hs Œπ hŒπ f hf => (mem_iff_iSup_of_isClosed hs).2 ‚ü®Œπ, hŒπ, f, hf, rfl‚ü©
  rw [‚Üê closure_subset_iff_isClosed]
  intro h x hx
  rcases mem_closure_iff_iSup.1 hx with ‚ü®Œπ, hŒπ, f, hf, rfl‚ü©
  exact h hŒπ f hf

theorem isClosed_iff_bsup :
    IsClosed s ‚Üî
      ‚àÄ {o : Ordinal}, o ‚â† 0 ‚Üí ‚àÄ f : ‚àÄ a < o, Ordinal,
        (‚àÄ i hi, f i hi ‚àà s) ‚Üí bsup.{u, u} o f ‚àà s := by
  rw [isClosed_iff_iSup]
  refine ‚ü®fun H o ho f hf => H (toType_nonempty_iff_ne_zero.2 ho) _ ?_, fun H Œπ hŒπ f hf => ?_‚ü©
  ¬∑ exact fun i => hf _ _
  ¬∑ rw [‚Üê bsup_eq_iSup]
    apply H (type_ne_zero_iff_nonempty.2 hŒπ)
    exact fun i hi => hf _

@[deprecated SuccOrder.isSuccLimit_of_mem_frontier (since := "2026-01-20")]
theorem isSuccLimit_of_mem_frontier (ha : a ‚àà frontier s) : IsSuccLimit a :=
  SuccOrder.isSuccLimit_of_mem_frontier ha

@[deprecated Order.isNormal_iff_strictMono_and_continuous (since := "2025-08-21")]
theorem isNormal_iff_strictMono_and_continuous (f : Ordinal.{u} ‚Üí Ordinal.{u}) :
    IsNormal f ‚Üî StrictMono f ‚àß Continuous f :=
  Order.isNormal_iff_strictMono_and_continuous

theorem enumOrd_isNormal_iff_isClosed (hs : ¬¨ BddAbove s) :
    IsNormal (enumOrd s) ‚Üî IsClosed s := by
  have Hs := enumOrd_strictMono hs
  refine
    ‚ü®fun h => isClosed_iff_iSup.2 fun {Œπ} hŒπ f hf => ?_, fun h =>
      isNormal_iff.2 ‚ü®Hs, fun a ha o H => ?_‚ü©‚ü©
  ¬∑ let g : Œπ ‚Üí Ordinal.{u} := fun i => (enumOrdOrderIso s hs).symm ‚ü®_, hf i‚ü©
    suffices enumOrd s (‚®Ü i, g i) = ‚®Ü i, f i by
      rw [‚Üê this]
      exact enumOrd_mem hs _
    rw [h.map_iSup (bddAbove_of_small _)]
    congr
    ext x
    change (enumOrdOrderIso s hs _).val = f x
    rw [OrderIso.apply_symm_apply]
  ¬∑ rw [isClosed_iff_bsup] at h
    suffices enumOrd s a ‚â§ bsup.{u, u} a fun b (_ : b < a) => enumOrd s b from
      this.trans (bsup_le H)
    obtain ‚ü®b, hb‚ü© := enumOrd_surjective hs (h ha.ne_bot (fun b _ => enumOrd s b)
      fun b _ => enumOrd_mem hs b)
    rw [‚Üê hb]
    apply Hs.monotone
    by_contra! hba
    apply (Hs (lt_succ b)).not_ge
    rw [hb]
    exact le_bsup.{u, u} _ _ (ha.succ_lt hba)

open Set Filter Set.Notation

/-- An ordinal is an accumulation point of a set of ordinals if it is positive and there
are elements in the set arbitrarily close to the ordinal from below. -/
def IsAcc (o : Ordinal) (S : Set Ordinal) : Prop :=
  AccPt o (ùìü S)

/-- A set of ordinals is closed below an ordinal if it contains all of
its accumulation points below the ordinal. -/
def IsClosedBelow (S : Set Ordinal) (o : Ordinal) : Prop :=
  IsClosed (Iio o ‚Üì‚à© S)

theorem isAcc_iff (o : Ordinal) (S : Set Ordinal) : o.IsAcc S ‚Üî
    o ‚â† 0 ‚àß ‚àÄ p < o, (S ‚à© Ioo p o).Nonempty := by
  dsimp [IsAcc]
  constructor
  ¬∑ rw [accPt_iff_nhds]
    intro h
    constructor
    ¬∑ rintro rfl
      obtain ‚ü®x, hx‚ü© := h (Iio 1) (Iio_mem_nhds zero_lt_one)
      exact hx.2 <| lt_one_iff_zero.mp hx.1.1
    ¬∑ intro p plt
      obtain ‚ü®x, hx‚ü© := h (Ioo p (o + 1)) <| Ioo_mem_nhds plt (lt_succ o)
      use x
      refine ‚ü®hx.1.2, ‚ü®hx.1.1.1, lt_of_le_of_ne ?_ hx.2‚ü©‚ü©
      have := hx.1.1.2
      rwa [‚Üê succ_eq_add_one, lt_succ_iff] at this
  ¬∑ rw [accPt_iff_nhds]
    intro h u umem
    obtain ‚ü®l, hl‚ü© := exists_Ioc_subset_of_mem_nhds umem ‚ü®0, pos_iff_ne_zero.mpr h.1‚ü©
    obtain ‚ü®x, hx‚ü© := h.2 l hl.1
    use x
    exact ‚ü®‚ü®hl.2 ‚ü®hx.2.1, hx.2.2.le‚ü©, hx.1‚ü©, hx.2.2.ne‚ü©

theorem IsAcc.forall_lt {o : Ordinal} {S : Set Ordinal} (h : o.IsAcc S) :
    ‚àÄ p < o, (S ‚à© Ioo p o).Nonempty := ((isAcc_iff _ _).mp h).2

theorem IsAcc.pos {o : Ordinal} {S : Set Ordinal} (h : o.IsAcc S) :
    0 < o := pos_iff_ne_zero.mpr ((isAcc_iff _ _).mp h).1

theorem IsAcc.isSuccLimit {o : Ordinal} {S : Set Ordinal} (h : o.IsAcc S) : IsSuccLimit o := by
  rw [isAcc_iff] at h
  rw [isSuccLimit_iff]
  refine ‚ü®h.1, isSuccPrelimit_of_succ_ne fun x hx ‚Ü¶ ?_‚ü©
  rcases h.2 x (lt_of_lt_of_le (lt_succ x) hx.le) with ‚ü®p, hp‚ü©
  exact (hx.symm ‚ñ∏ (succ_le_iff.mpr hp.2.1)).not_gt hp.2.2

theorem IsAcc.mono {o : Ordinal} {S T : Set Ordinal} (h : S ‚äÜ T) (ho : o.IsAcc S) :
    o.IsAcc T := by
  rw [isAcc_iff] at *
  exact ‚ü®ho.1, fun p plto ‚Ü¶ (ho.2 p plto).casesOn fun s hs ‚Ü¶ ‚ü®s, h hs.1, hs.2‚ü©‚ü©

theorem IsAcc.inter_Ioo_nonempty {o : Ordinal} {S : Set Ordinal} (hS : o.IsAcc S)
    {p : Ordinal} (hp : p < o) : (S ‚à© Ioo p o).Nonempty := hS.forall_lt p hp

-- todo: prove this for a general linear `SuccOrder`.
theorem accPt_subtype {p o : Ordinal} (S : Set Ordinal) (hpo : p < o) :
    AccPt p (ùìü S) ‚Üî AccPt ‚ü®p, hpo‚ü© (ùìü (Iio o ‚Üì‚à© S)) := by
  constructor
  ¬∑ intro h
    have plim := IsAcc.isSuccLimit h
    rw [accPt_iff_nhds] at *
    intro u hu
    obtain ‚ü®l, hl‚ü© := exists_Ioc_subset_of_mem_nhds hu ‚ü®‚ü®0, plim.bot_lt.trans hpo‚ü©, plim.bot_lt‚ü©
    obtain ‚ü®x, hx‚ü© := h (Ioo l (p + 1)) (Ioo_mem_nhds hl.1 (lt_add_one _))
    use ‚ü®x, lt_of_le_of_lt (lt_succ_iff.mp hx.1.1.2) hpo‚ü©
    refine ‚ü®?_, Subtype.coe_ne_coe.mp hx.2‚ü©
    exact ‚ü®hl.2 ‚ü®hx.1.1.1, by exact_mod_cast lt_succ_iff.mp hx.1.1.2‚ü©, hx.1.2‚ü©
  ¬∑ intro h
    rw [accPt_iff_nhds] at *
    intro u hu
    by_cases ho : p + 1 < o
    ¬∑ have ppos : p ‚â† 0 := by
        rintro rfl
        rw [zero_add] at ho
        specialize h (Iio ‚ü®1, ho‚ü©) (Iio_mem_nhds (Subtype.mk_lt_mk.mpr zero_lt_one))
        obtain ‚ü®_, h‚ü© := h
        exact h.2 <| Subtype.mk_eq_mk.mpr (lt_one_iff_zero.mp h.1.1)
      have plim : IsSuccLimit p := by
        contrapose! h
        obtain ‚ü®q, hq‚ü© := ((zero_or_succ_or_isSuccLimit p).resolve_left ppos).resolve_right h
        use (Ioo ‚ü®q, ((hq ‚ñ∏ lt_succ q).trans hpo)‚ü© ‚ü®p + 1, ho‚ü©)
        constructor
        ¬∑ exact Ioo_mem_nhds (hq ‚ñ∏ lt_succ q) (lt_succ p)
        ¬∑ intro _ mem
          have aux1 := Subtype.mk_lt_mk.mp mem.1.1
          have aux2 := Subtype.mk_lt_mk.mp mem.1.2
          rw [Subtype.mk_eq_mk]
          subst hq
          exact ((succ_le_iff.mpr aux1).antisymm (le_of_lt_succ aux2)).symm
      obtain ‚ü®l, hl‚ü© := exists_Ioc_subset_of_mem_nhds hu ‚ü®0, plim.bot_lt‚ü©
      obtain ‚ü®x, hx‚ü© := h (Ioo ‚ü®l, hl.1.trans hpo‚ü© ‚ü®p + 1, ho‚ü©) (Ioo_mem_nhds hl.1 (lt_add_one p))
      use x
      exact ‚ü®‚ü®hl.2 ‚ü®hx.1.1.1, lt_succ_iff.mp hx.1.1.2‚ü©, hx.1.2‚ü©, fun h ‚Ü¶ hx.2 (SetCoe.ext h)‚ü©
    have hp : o = p + 1 := (le_succ_iff_eq_or_le.mp (le_of_not_gt ho)).resolve_right
      (not_le_of_gt hpo)
    have ppos : p ‚â† 0 := by
      rintro rfl
      obtain ‚ü®x, hx‚ü© := h Set.univ univ_mem
      have : ‚Üëx < o := x.2
      simp_rw [hp, zero_add, lt_one_iff_zero] at this
      exact hx.2 (SetCoe.ext this)
    obtain ‚ü®l, hl‚ü© := exists_Ioc_subset_of_mem_nhds hu ‚ü®0, pos_iff_ne_zero.mpr ppos‚ü©
    obtain ‚ü®x, hx‚ü© := h (Ioi ‚ü®l, hl.1.trans hpo‚ü©) (Ioi_mem_nhds hl.1)
    use x
    refine ‚ü®‚ü®hl.2 ‚ü®hx.1.1, ?_‚ü©, hx.1.2‚ü©, fun h ‚Ü¶ hx.2 (SetCoe.ext h)‚ü©
    rw [‚Üê lt_add_one_iff, ‚Üê hp]
    exact x.2

theorem isClosedBelow_iff {S : Set Ordinal} {o : Ordinal} : IsClosedBelow S o ‚Üî
    ‚àÄ p < o, IsAcc p S ‚Üí p ‚àà S := by
  dsimp [IsClosedBelow]
  constructor
  ¬∑ intro h p plto hp
    have : AccPt ‚ü®p, plto‚ü© (ùìü (Iio o ‚Üì‚à© S)) := (accPt_subtype _ _).mp hp
    rw [isClosed_iff_clusterPt] at h
    exact h ‚ü®p, plto‚ü© this.clusterPt
  ¬∑ intro h
    rw [isClosed_iff_clusterPt]
    intro r hr
    match clusterPt_principal.mp hr with
    | .inl h => exact h
    | .inr h' => exact h r.1 r.2 <| (accPt_subtype _ _).mpr h'

alias ‚ü®IsClosedBelow.forall_lt, _‚ü© := isClosedBelow_iff

theorem IsClosedBelow.sInter {o : Ordinal} {S : Set (Set Ordinal)}
    (h : ‚àÄ C ‚àà S, IsClosedBelow C o) : IsClosedBelow (‚ãÇ‚ÇÄ S) o := by
  rw [isClosedBelow_iff]
  intro p plto pAcc C CmemS
  exact (h C CmemS).forall_lt p plto (pAcc.mono (sInter_subset_of_mem CmemS))

theorem IsClosedBelow.iInter {Œπ : Type u} {f : Œπ ‚Üí Set Ordinal} {o : Ordinal}
    (h : ‚àÄ i, IsClosedBelow (f i) o) : IsClosedBelow (‚ãÇ i, f i) o :=
  IsClosedBelow.sInter fun _ ‚ü®i, hi‚ü© ‚Ü¶ hi ‚ñ∏ (h i)

end Ordinal
