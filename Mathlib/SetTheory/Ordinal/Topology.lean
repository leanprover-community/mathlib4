/-
Copyright (c) 2022 Violeta HernÃ¡ndez Palacios. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Violeta HernÃ¡ndez Palacios
-/
import Mathlib.SetTheory.Ordinal.Enum
import Mathlib.Tactic.TFAE
import Mathlib.Topology.Order.Monotone

/-!
### Topology of ordinals

We prove some miscellaneous results involving the order topology of ordinals.

### Main results

* `Ordinal.isClosed_iff_iSup` / `Ordinal.isClosed_iff_bsup`: A set of ordinals is closed iff it's
  closed under suprema.
* `Ordinal.isNormal_iff_strictMono_and_continuous`: A characterization of normal ordinal
  functions.
* `Ordinal.enumOrd_isNormal_iff_isClosed`: The function enumerating the ordinals of a set is
  normal iff the set is closed.
-/


noncomputable section

universe u v

open Cardinal Order Topology

namespace Ordinal

variable {s : Set Ordinal.{u}} {a : Ordinal.{u}}

instance : TopologicalSpace Ordinal.{u} := Preorder.topology Ordinal.{u}
instance : OrderTopology Ordinal.{u} := âŸ¨rflâŸ©

theorem isOpen_singleton_iff : IsOpen ({a} : Set Ordinal) â†” Â¬IsLimit a := by
  refine âŸ¨fun h ha => ?_, fun ha => ?_âŸ©
  Â· obtain âŸ¨b, c, hbc, hbc'âŸ© :=
      (mem_nhds_iff_exists_Ioo_subset' âŸ¨0, ha.posâŸ© âŸ¨_, lt_succ aâŸ©).1
        (h.mem_nhds rfl)
    have hba := ha.succ_lt hbc.1
    exact hba.ne (hbc' âŸ¨lt_succ b, hba.trans hbc.2âŸ©)
  Â· rcases zero_or_succ_or_limit a with (rfl | âŸ¨b, rflâŸ© | ha')
    Â· rw [â† bot_eq_zero, â† Set.Iic_bot, â† Iio_succ]
      exact isOpen_Iio
    Â· rw [â† Set.Icc_self, Icc_succ_left, â† Ioo_succ_right]
      exact isOpen_Ioo
    Â· exact (ha ha').elim

@[deprecated SuccOrder.nhdsGT (since := "2025-01-05")]
protected theorem nhdsGT (a : Ordinal) : ğ“[>] a = âŠ¥ := SuccOrder.nhdsGT

@[deprecated (since := "2024-12-22")] alias nhds_right' := Ordinal.nhdsGT

@[deprecated SuccOrder.nhdsLT_eq_nhdsNE (since := "2025-01-05")]
theorem nhdsLT_eq_nhdsNE (a : Ordinal) : ğ“[<] a = ğ“[â‰ ] a :=
  SuccOrder.nhdsLT_eq_nhdsNE a

@[deprecated (since := "2024-12-22")] alias nhds_left'_eq_nhds_ne := nhdsLT_eq_nhdsNE

@[deprecated SuccOrder.nhdsLE_eq_nhds (since := "2025-01-05")]
theorem nhdsLE_eq_nhds (a : Ordinal) : ğ“[â‰¤] a = ğ“ a :=
  SuccOrder.nhdsLE_eq_nhds a

@[deprecated (since := "2024-12-22")] alias nhds_left_eq_nhds := nhdsLE_eq_nhds

@[deprecated SuccOrder.hasBasis_nhds_Ioc_of_exists_lt (since := "2025-01-05")]
theorem hasBasis_nhds_Ioc (h : a â‰  0) : (ğ“ a).HasBasis (Â· < a) (Set.Ioc Â· a) :=
  SuccOrder.hasBasis_nhds_Ioc_of_exists_lt âŸ¨0, Ordinal.pos_iff_ne_zero.2 hâŸ©

@[deprecated (since := "2024-12-22")] alias nhdsBasis_Ioc := hasBasis_nhds_Ioc

-- todo: generalize to a `SuccOrder`
theorem nhds_eq_pure : ğ“ a = pure a â†” Â¬IsLimit a :=
  (isOpen_singleton_iff_nhds_eq_pure _).symm.trans isOpen_singleton_iff

-- todo: generalize `Ordinal.IsLimit` and this lemma to a `SuccOrder`
theorem isOpen_iff : IsOpen s â†” âˆ€ o âˆˆ s, IsLimit o â†’ âˆƒ a < o, Set.Ioo a o âŠ† s := by
  refine isOpen_iff_mem_nhds.trans <| forallâ‚‚_congr fun o ho => ?_
  by_cases ho' : IsLimit o
  Â· simp only [(SuccOrder.hasBasis_nhds_Ioc_of_exists_lt âŸ¨0, ho'.posâŸ©).mem_iff, ho', true_implies]
    refine exists_congr fun a => and_congr_right fun ha => ?_
    simp only [â† Set.Ioo_insert_right ha, Set.insert_subset_iff, ho, true_and]
  Â· simp [nhds_eq_pure.2 ho', ho, ho']

open List Set in
theorem mem_closure_tfae (a : Ordinal.{u}) (s : Set Ordinal) :
    TFAE [a âˆˆ closure s,
      a âˆˆ closure (s âˆ© Iic a),
      (s âˆ© Iic a).Nonempty âˆ§ sSup (s âˆ© Iic a) = a,
      âˆƒ t, t âŠ† s âˆ§ t.Nonempty âˆ§ BddAbove t âˆ§ sSup t = a,
      âˆƒ (o : Ordinal.{u}), o â‰  0 âˆ§ âˆƒ (f : âˆ€ x < o, Ordinal),
        (âˆ€ x hx, f x hx âˆˆ s) âˆ§ bsup.{u, u} o f = a,
      âˆƒ (Î¹ : Type u), Nonempty Î¹ âˆ§ âˆƒ f : Î¹ â†’ Ordinal, (âˆ€ i, f i âˆˆ s) âˆ§ â¨† i, f i = a] := by
  tfae_have 1 â†’ 2 := by
    simpa only [mem_closure_iff_nhdsWithin_neBot, inter_comm s, nhdsWithin_inter',
      SuccOrder.nhdsLE_eq_nhds] using id
  tfae_have 2 â†’ 3
  | h => by
    rcases (s âˆ© Iic a).eq_empty_or_nonempty with he | hne
    Â· simp [he] at h
    Â· refine âŸ¨hne, (isLUB_of_mem_closure ?_ h).csSup_eq hneâŸ©
      exact fun x hx => hx.2
  tfae_have 3 â†’ 4
  | h => âŸ¨_, inter_subset_left, h.1, bddAbove_Iic.mono inter_subset_right, h.2âŸ©
  tfae_have 4 â†’ 5 := by
    rintro âŸ¨t, hts, hne, hbdd, rflâŸ©
    have hlub : IsLUB t (sSup t) := isLUB_csSup hne hbdd
    let âŸ¨y, hytâŸ© := hne
    classical
      refine âŸ¨succ (sSup t), succ_ne_zero _, fun x _ => if x âˆˆ t then x else y, fun x _ => ?_, ?_âŸ©
      Â· simp only
        split_ifs with h <;> exact hts â€¹_â€º
      Â· refine le_antisymm (bsup_le fun x _ => ?_) (csSup_le hne fun x hx => ?_)
        Â· split_ifs <;> exact hlub.1 â€¹_â€º
        Â· refine (if_pos hx).symm.trans_le (le_bsup _ _ <| (hlub.1 hx).trans_lt (lt_succ _))
  tfae_have 5 â†’ 6 := by
    rintro âŸ¨o, hâ‚€, f, hfs, rflâŸ©
    exact âŸ¨_, toType_nonempty_iff_ne_zero.2 hâ‚€, familyOfBFamily o f, fun _ => hfs _ _, rflâŸ©
  tfae_have 6 â†’ 1 := by
    rintro âŸ¨Î¹, hne, f, hfs, rflâŸ©
    exact closure_mono (range_subset_iff.2 hfs) <| csSup_mem_closure (range_nonempty f)
      (bddAbove_range.{u, u} f)
  tfae_finish

theorem mem_closure_iff_iSup :
    a âˆˆ closure s â†”
      âˆƒ (Î¹ : Type u) (_ : Nonempty Î¹) (f : Î¹ â†’ Ordinal), (âˆ€ i, f i âˆˆ s) âˆ§ â¨† i, f i = a := by
  apply ((mem_closure_tfae a s).out 0 5).trans
  simp_rw [exists_prop]

theorem mem_iff_iSup_of_isClosed (hs : IsClosed s) :
    a âˆˆ s â†” âˆƒ (Î¹ : Type u) (_hÎ¹ : Nonempty Î¹) (f : Î¹ â†’ Ordinal),
      (âˆ€ i, f i âˆˆ s) âˆ§ â¨† i, f i = a := by
  rw [â† mem_closure_iff_iSup, hs.closure_eq]

theorem mem_closure_iff_bsup :
    a âˆˆ closure s â†”
      âˆƒ (o : Ordinal) (_ho : o â‰  0) (f : âˆ€ a < o, Ordinal),
        (âˆ€ i hi, f i hi âˆˆ s) âˆ§ bsup.{u, u} o f = a := by
  apply ((mem_closure_tfae a s).out 0 4).trans
  simp_rw [exists_prop]

theorem mem_closed_iff_bsup (hs : IsClosed s) :
    a âˆˆ s â†”
      âˆƒ (o : Ordinal) (_ho : o â‰  0) (f : âˆ€ a < o, Ordinal),
        (âˆ€ i hi, f i hi âˆˆ s) âˆ§ bsup.{u, u} o f = a := by
  rw [â† mem_closure_iff_bsup, hs.closure_eq]

theorem isClosed_iff_iSup :
    IsClosed s â†”
      âˆ€ {Î¹ : Type u}, Nonempty Î¹ â†’ âˆ€ f : Î¹ â†’ Ordinal, (âˆ€ i, f i âˆˆ s) â†’ â¨† i, f i âˆˆ s := by
  use fun hs Î¹ hÎ¹ f hf => (mem_iff_iSup_of_isClosed hs).2 âŸ¨Î¹, hÎ¹, f, hf, rflâŸ©
  rw [â† closure_subset_iff_isClosed]
  intro h x hx
  rcases mem_closure_iff_iSup.1 hx with âŸ¨Î¹, hÎ¹, f, hf, rflâŸ©
  exact h hÎ¹ f hf

theorem isClosed_iff_bsup :
    IsClosed s â†”
      âˆ€ {o : Ordinal}, o â‰  0 â†’ âˆ€ f : âˆ€ a < o, Ordinal,
        (âˆ€ i hi, f i hi âˆˆ s) â†’ bsup.{u, u} o f âˆˆ s := by
  rw [isClosed_iff_iSup]
  refine âŸ¨fun H o ho f hf => H (toType_nonempty_iff_ne_zero.2 ho) _ ?_, fun H Î¹ hÎ¹ f hf => ?_âŸ©
  Â· exact fun i => hf _ _
  Â· rw [â† Ordinal.sup, â† bsup_eq_sup]
    apply H (type_ne_zero_iff_nonempty.2 hÎ¹)
    exact fun i hi => hf _

theorem isLimit_of_mem_frontier (ha : a âˆˆ frontier s) : IsLimit a := by
  simp only [frontier_eq_closure_inter_closure, Set.mem_inter_iff, mem_closure_iff] at ha
  by_contra h
  rw [â† isOpen_singleton_iff] at h
  rcases ha.1 _ h rfl with âŸ¨b, hb, hb'âŸ©
  rcases ha.2 _ h rfl with âŸ¨c, hc, hc'âŸ©
  rw [Set.mem_singleton_iff] at *
  subst hb; subst hc
  exact hc' hb'

theorem isNormal_iff_strictMono_and_continuous (f : Ordinal.{u} â†’ Ordinal.{u}) :
    IsNormal f â†” StrictMono f âˆ§ Continuous f := by
  refine âŸ¨fun h => âŸ¨h.strictMono, ?_âŸ©, ?_âŸ©
  Â· rw [continuous_def]
    intro s hs
    rw [isOpen_iff] at *
    intro o ho ho'
    rcases hs _ ho (h.isLimit ho') with âŸ¨a, ha, hasâŸ©
    rw [â† IsNormal.bsup_eq.{u, u} h ho', lt_bsup] at ha
    rcases ha with âŸ¨b, hb, habâŸ©
    exact
      âŸ¨b, hb, fun c hc =>
        Set.mem_preimage.2 (has âŸ¨hab.trans (h.strictMono hc.1), h.strictMono hc.2âŸ©)âŸ©
  Â· rw [isNormal_iff_strictMono_limit]
    rintro âŸ¨h, h'âŸ©
    refine âŸ¨h, fun o ho a h => ?_âŸ©
    suffices o âˆˆ f â»Â¹' Set.Iic a from Set.mem_preimage.1 this
    rw [mem_iff_iSup_of_isClosed (IsClosed.preimage h' (@isClosed_Iic _ _ _ _ a))]
    exact
      âŸ¨_, toType_nonempty_iff_ne_zero.2 ho.ne_zero, typein (Â· < Â·), fun i => h _ (typein_lt_self i),
        sup_typein_limit fun _ â†¦ ho.succ_ltâŸ©

theorem enumOrd_isNormal_iff_isClosed (hs : Â¬ BddAbove s) :
    IsNormal (enumOrd s) â†” IsClosed s := by
  have Hs := enumOrd_strictMono hs
  refine
    âŸ¨fun h => isClosed_iff_iSup.2 fun {Î¹} hÎ¹ f hf => ?_, fun h =>
      (isNormal_iff_strictMono_limit _).2 âŸ¨Hs, fun a ha o H => ?_âŸ©âŸ©
  Â· let g : Î¹ â†’ Ordinal.{u} := fun i => (enumOrdOrderIso s hs).symm âŸ¨_, hf iâŸ©
    suffices enumOrd s (â¨† i, g i) = â¨† i, f i by
      rw [â† this]
      exact enumOrd_mem hs _
    rw [IsNormal.map_iSup h g]
    congr
    ext x
    change (enumOrdOrderIso s hs _).val = f x
    rw [OrderIso.apply_symm_apply]
  Â· rw [isClosed_iff_bsup] at h
    suffices enumOrd s a â‰¤ bsup.{u, u} a fun b (_ : b < a) => enumOrd s b from
      this.trans (bsup_le H)
    obtain âŸ¨b, hbâŸ© := enumOrd_surjective hs (h ha.ne_zero (fun b _ => enumOrd s b)
      fun b _ => enumOrd_mem hs b)
    rw [â† hb]
    apply Hs.monotone
    by_contra! hba
    apply (Hs (lt_succ b)).not_ge
    rw [hb]
    exact le_bsup.{u, u} _ _ (ha.succ_lt hba)

open Set Filter Set.Notation

/-- An ordinal is an accumulation point of a set of ordinals if it is positive and there
are elements in the set arbitrarily close to the ordinal from below. -/
def IsAcc (o : Ordinal) (S : Set Ordinal) : Prop :=
  AccPt o (ğ“Ÿ S)

/-- A set of ordinals is closed below an ordinal if it contains all of
its accumulation points below the ordinal. -/
def IsClosedBelow (S : Set Ordinal) (o : Ordinal) : Prop :=
  IsClosed (Iio o â†“âˆ© S)

theorem isAcc_iff (o : Ordinal) (S : Set Ordinal) : o.IsAcc S â†”
    o â‰  0 âˆ§ âˆ€ p < o, (S âˆ© Ioo p o).Nonempty := by
  dsimp [IsAcc]
  constructor
  Â· rw [accPt_iff_nhds]
    intro h
    constructor
    Â· rintro rfl
      obtain âŸ¨x, hxâŸ© := h (Iio 1) (Iio_mem_nhds zero_lt_one)
      exact hx.2 <| lt_one_iff_zero.mp hx.1.1
    Â· intro p plt
      obtain âŸ¨x, hxâŸ© := h (Ioo p (o + 1)) <| Ioo_mem_nhds plt (lt_succ o)
      use x
      refine âŸ¨hx.1.2, âŸ¨hx.1.1.1, lt_of_le_of_ne ?_ hx.2âŸ©âŸ©
      have := hx.1.1.2
      rwa [â† succ_eq_add_one, lt_succ_iff] at this
  Â· rw [accPt_iff_nhds]
    intro h u umem
    obtain âŸ¨l, hlâŸ© := exists_Ioc_subset_of_mem_nhds umem âŸ¨0, Ordinal.pos_iff_ne_zero.mpr h.1âŸ©
    obtain âŸ¨x, hxâŸ© := h.2 l hl.1
    use x
    exact âŸ¨âŸ¨hl.2 âŸ¨hx.2.1, hx.2.2.leâŸ©, hx.1âŸ©, hx.2.2.neâŸ©

theorem IsAcc.forall_lt {o : Ordinal} {S : Set Ordinal} (h : o.IsAcc S) :
    âˆ€ p < o, (S âˆ© Ioo p o).Nonempty := ((isAcc_iff _ _).mp h).2

theorem IsAcc.pos {o : Ordinal} {S : Set Ordinal} (h : o.IsAcc S) :
    0 < o := Ordinal.pos_iff_ne_zero.mpr ((isAcc_iff _ _).mp h).1

theorem IsAcc.isLimit {o : Ordinal} {S : Set Ordinal} (h : o.IsAcc S) : IsLimit o := by
  rw [isAcc_iff] at h
  refine isLimit_of_not_succ_of_ne_zero (fun âŸ¨x, hxâŸ© â†¦ ?_) h.1
  rcases h.2 x (lt_of_lt_of_le (lt_succ x) hx.symm.le) with âŸ¨p, hpâŸ©
  exact (hx.symm â–¸ (succ_le_iff.mpr hp.2.1)).not_gt hp.2.2

theorem IsAcc.mono {o : Ordinal} {S T : Set Ordinal} (h : S âŠ† T) (ho : o.IsAcc S) :
    o.IsAcc T := by
  rw [isAcc_iff] at *
  exact âŸ¨ho.1, fun p plto â†¦ (ho.2 p plto).casesOn fun s hs â†¦ âŸ¨s, h hs.1, hs.2âŸ©âŸ©

theorem IsAcc.inter_Ioo_nonempty {o : Ordinal} {S : Set Ordinal} (hS : o.IsAcc S)
    {p : Ordinal} (hp : p < o) : (S âˆ© Ioo p o).Nonempty := hS.forall_lt p hp

-- todo: prove this for a general linear `SuccOrder`.
theorem accPt_subtype {p o : Ordinal} (S : Set Ordinal) (hpo : p < o) :
    AccPt p (ğ“Ÿ S) â†” AccPt âŸ¨p, hpoâŸ© (ğ“Ÿ (Iio o â†“âˆ© S)) := by
  constructor
  Â· intro h
    have plim : p.IsLimit := IsAcc.isLimit h
    rw [accPt_iff_nhds] at *
    intro u hu
    obtain âŸ¨l, hlâŸ© := exists_Ioc_subset_of_mem_nhds hu âŸ¨âŸ¨0, plim.pos.trans hpoâŸ©, plim.posâŸ©
    obtain âŸ¨x, hxâŸ© := h (Ioo l (p + 1)) (Ioo_mem_nhds hl.1 (lt_add_one _))
    use âŸ¨x, lt_of_le_of_lt (lt_succ_iff.mp hx.1.1.2) hpoâŸ©
    refine âŸ¨?_, Subtype.coe_ne_coe.mp hx.2âŸ©
    exact âŸ¨hl.2 âŸ¨hx.1.1.1, by exact_mod_cast lt_succ_iff.mp hx.1.1.2âŸ©, hx.1.2âŸ©
  Â· intro h
    rw [accPt_iff_nhds] at *
    intro u hu
    by_cases ho : p + 1 < o
    Â· have ppos : p â‰  0 := by
        rintro rfl
        rw [zero_add] at ho
        specialize h (Iio âŸ¨1, hoâŸ©) (Iio_mem_nhds (Subtype.mk_lt_mk.mpr zero_lt_one))
        obtain âŸ¨_, hâŸ© := h
        exact h.2 <| Subtype.mk_eq_mk.mpr (lt_one_iff_zero.mp h.1.1)
      have plim : p.IsLimit := by
        contrapose! h
        obtain âŸ¨q, hqâŸ© := ((zero_or_succ_or_limit p).resolve_left ppos).resolve_right h
        use (Ioo âŸ¨q, ((hq â–¸ lt_succ q).trans hpo)âŸ© âŸ¨p + 1, hoâŸ©)
        constructor
        Â· exact Ioo_mem_nhds (by simp only [hq, Subtype.mk_lt_mk, lt_succ]) (lt_succ p)
        Â· intro _ mem
          have aux1 := Subtype.mk_lt_mk.mp mem.1.1
          have aux2 := Subtype.mk_lt_mk.mp mem.1.2
          rw [Subtype.mk_eq_mk]
          rw [hq] at aux2 âŠ¢
          exact ((succ_le_iff.mpr aux1).antisymm (le_of_lt_succ aux2)).symm
      obtain âŸ¨l, hlâŸ© := exists_Ioc_subset_of_mem_nhds hu âŸ¨0, plim.posâŸ©
      obtain âŸ¨x, hxâŸ© := h (Ioo âŸ¨l, hl.1.trans hpoâŸ© âŸ¨p + 1, hoâŸ©) (Ioo_mem_nhds hl.1 (lt_add_one p))
      use x
      exact âŸ¨âŸ¨hl.2 âŸ¨hx.1.1.1, lt_succ_iff.mp hx.1.1.2âŸ©, hx.1.2âŸ©, fun h â†¦ hx.2 (SetCoe.ext h)âŸ©
    have hp : o = p + 1 := (le_succ_iff_eq_or_le.mp (le_of_not_gt ho)).resolve_right
      (not_le_of_gt hpo)
    have ppos : p â‰  0 := by
      rintro rfl
      obtain âŸ¨x, hxâŸ© := h Set.univ univ_mem
      have : â†‘x < o := x.2
      simp_rw [hp, zero_add, lt_one_iff_zero] at this
      exact hx.2 (SetCoe.ext this)
    obtain âŸ¨l, hlâŸ© := exists_Ioc_subset_of_mem_nhds hu âŸ¨0, Ordinal.pos_iff_ne_zero.mpr pposâŸ©
    obtain âŸ¨x, hxâŸ© := h (Ioi âŸ¨l, hl.1.trans hpoâŸ©) (Ioi_mem_nhds hl.1)
    use x
    refine âŸ¨âŸ¨hl.2 âŸ¨hx.1.1, ?_âŸ©, hx.1.2âŸ©, fun h â†¦ hx.2 (SetCoe.ext h)âŸ©
    rw [â† lt_add_one_iff, â† hp]
    exact x.2

theorem isClosedBelow_iff {S : Set Ordinal} {o : Ordinal} : IsClosedBelow S o â†”
    âˆ€ p < o, IsAcc p S â†’ p âˆˆ S := by
  dsimp [IsClosedBelow]
  constructor
  Â· intro h p plto hp
    have : AccPt âŸ¨p, pltoâŸ© (ğ“Ÿ (Iio o â†“âˆ© S)) := (accPt_subtype _ _).mp hp
    rw [isClosed_iff_clusterPt] at h
    exact h âŸ¨p, pltoâŸ© this.clusterPt
  Â· intro h
    rw [isClosed_iff_clusterPt]
    intro r hr
    match clusterPt_principal.mp hr with
    | .inl h => exact h
    | .inr h' => exact h r.1 r.2 <| (accPt_subtype _ _).mpr h'

alias âŸ¨IsClosedBelow.forall_lt, _âŸ© := isClosedBelow_iff

theorem IsClosedBelow.sInter {o : Ordinal} {S : Set (Set Ordinal)}
    (h : âˆ€ C âˆˆ S, IsClosedBelow C o) : IsClosedBelow (â‹‚â‚€ S) o := by
  rw [isClosedBelow_iff]
  intro p plto pAcc C CmemS
  exact (h C CmemS).forall_lt p plto (pAcc.mono (sInter_subset_of_mem CmemS))

theorem IsClosedBelow.iInter {Î¹ : Type u} {f : Î¹ â†’ Set Ordinal} {o : Ordinal}
    (h : âˆ€ i, IsClosedBelow (f i) o) : IsClosedBelow (â‹‚ i, f i) o :=
  IsClosedBelow.sInter fun _ âŸ¨i, hiâŸ© â†¦ hi â–¸ (h i)

end Ordinal
