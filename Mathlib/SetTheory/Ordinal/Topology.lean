/-
Copyright (c) 2022 Violeta HernÃ¡ndez Palacios. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Violeta HernÃ¡ndez Palacios
-/
import Mathlib.SetTheory.Ordinal.Arithmetic
import Mathlib.Tactic.TFAE
import Mathlib.Topology.Order.Monotone

#align_import set_theory.ordinal.topology from "leanprover-community/mathlib"@"740acc0e6f9adf4423f92a485d0456fc271482da"

/-!
### Topology of ordinals

We prove some miscellaneous results involving the order topology of ordinals.

### Main results

* `Ordinal.isClosed_iff_sup` / `Ordinal.isClosed_iff_bsup`: A set of ordinals is closed iff it's
  closed under suprema.
* `Ordinal.isNormal_iff_strictMono_and_continuous`: A characterization of normal ordinal
  functions.
* `Ordinal.enumOrd_isNormal_iff_isClosed`: The function enumerating the ordinals of a set is
  normal iff the set is closed.
-/


noncomputable section

universe u v

open Cardinal Order Topology

namespace Ordinal

variable {s : Set Ordinal.{u}} {a : Ordinal.{u}}

instance : TopologicalSpace Ordinal.{u} := Preorder.topology Ordinal.{u}
instance : OrderTopology Ordinal.{u} := âŸ¨rflâŸ©

theorem isOpen_singleton_iff : IsOpen ({a} : Set Ordinal) â†” Â¬IsLimit a := by
  refine âŸ¨fun h âŸ¨hâ‚€, hsuccâŸ© => ?_, fun ha => ?_âŸ©
  Â· obtain âŸ¨b, c, hbc, hbc'âŸ© :=
      (mem_nhds_iff_exists_Ioo_subset' âŸ¨0, Ordinal.pos_iff_ne_zero.2 hâ‚€âŸ© âŸ¨_, lt_succ aâŸ©).1
        (h.mem_nhds rfl)
    have hba := hsucc b hbc.1
    exact hba.ne (hbc' âŸ¨lt_succ b, hba.trans hbc.2âŸ©)
  Â· rcases zero_or_succ_or_limit a with (rfl | âŸ¨b, rflâŸ© | ha')
    Â· rw [â† bot_eq_zero, â† Set.Iic_bot, â† Iio_succ]
      exact isOpen_Iio
    Â· rw [â† Set.Icc_self, Icc_succ_left, â† Ioo_succ_right]
      exact isOpen_Ioo
    Â· exact (ha ha').elim
#align ordinal.is_open_singleton_iff Ordinal.isOpen_singleton_iff

-- Porting note (#11215): TODO: generalize to a `SuccOrder`
theorem nhds_right' (a : Ordinal) : ğ“[>] a = âŠ¥ := (covBy_succ a).nhdsWithin_Ioi

-- todo: generalize to a `SuccOrder`
theorem nhds_left'_eq_nhds_ne (a : Ordinal) : ğ“[<] a = ğ“[â‰ ] a := by
  rw [â† nhds_left'_sup_nhds_right', nhds_right', sup_bot_eq]

-- todo: generalize to a `SuccOrder`
theorem nhds_left_eq_nhds (a : Ordinal) : ğ“[â‰¤] a = ğ“ a := by
  rw [â† nhds_left_sup_nhds_right', nhds_right', sup_bot_eq]

-- todo: generalize to a `SuccOrder`
theorem nhdsBasis_Ioc (h : a â‰  0) : (ğ“ a).HasBasis (Â· < a) (Set.Ioc Â· a) :=
  nhds_left_eq_nhds a â–¸ nhdsWithin_Iic_basis' âŸ¨0, h.bot_ltâŸ©

-- todo: generalize to a `SuccOrder`
theorem nhds_eq_pure : ğ“ a = pure a â†” Â¬IsLimit a :=
  (isOpen_singleton_iff_nhds_eq_pure _).symm.trans isOpen_singleton_iff

-- todo: generalize `Ordinal.IsLimit` and this lemma to a `SuccOrder`
theorem isOpen_iff : IsOpen s â†” âˆ€ o âˆˆ s, IsLimit o â†’ âˆƒ a < o, Set.Ioo a o âŠ† s := by
  refine isOpen_iff_mem_nhds.trans <| forallâ‚‚_congr fun o ho => ?_
  by_cases ho' : IsLimit o
  Â· simp only [(nhdsBasis_Ioc ho'.1).mem_iff, ho', true_implies]
    refine exists_congr fun a => and_congr_right fun ha => ?_
    simp only [â† Set.Ioo_insert_right ha, Set.insert_subset_iff, ho, true_and]
  Â· simp [nhds_eq_pure.2 ho', ho, ho']
#align ordinal.is_open_iff Ordinal.isOpen_iff

open List Set in
theorem mem_closure_tfae (a : Ordinal.{u}) (s : Set Ordinal) :
    TFAE [a âˆˆ closure s,
      a âˆˆ closure (s âˆ© Iic a),
      (s âˆ© Iic a).Nonempty âˆ§ sSup (s âˆ© Iic a) = a,
      âˆƒ t, t âŠ† s âˆ§ t.Nonempty âˆ§ BddAbove t âˆ§ sSup t = a,
      âˆƒ (o : Ordinal.{u}), o â‰  0 âˆ§ âˆƒ (f : âˆ€ x < o, Ordinal),
        (âˆ€ x hx, f x hx âˆˆ s) âˆ§ bsup.{u, u} o f = a,
      âˆƒ (Î¹ : Type u), Nonempty Î¹ âˆ§ âˆƒ f : Î¹ â†’ Ordinal, (âˆ€ i, f i âˆˆ s) âˆ§ sup.{u, u} f = a] := by
  tfae_have 1 â†’ 2
  Â· simp only [mem_closure_iff_nhdsWithin_neBot, inter_comm s, nhdsWithin_inter', nhds_left_eq_nhds]
    exact id
  tfae_have 2 â†’ 3
  Â· intro h
    rcases (s âˆ© Iic a).eq_empty_or_nonempty with he | hne
    Â· simp [he] at h
    Â· refine âŸ¨hne, (isLUB_of_mem_closure ?_ h).csSup_eq hneâŸ©
      exact fun x hx => hx.2
  tfae_have 3 â†’ 4
  Â· exact fun h => âŸ¨_, inter_subset_left, h.1, bddAbove_Iic.mono inter_subset_right, h.2âŸ©
  tfae_have 4 â†’ 5
  Â· rintro âŸ¨t, hts, hne, hbdd, rflâŸ©
    have hlub : IsLUB t (sSup t) := isLUB_csSup hne hbdd
    let âŸ¨y, hytâŸ© := hne
    classical
      refine âŸ¨succ (sSup t), succ_ne_zero _, fun x _ => if x âˆˆ t then x else y, fun x _ => ?_, ?_âŸ©
      Â· simp only
        split_ifs with h <;> exact hts â€¹_â€º
      Â· refine le_antisymm (bsup_le fun x _ => ?_) (csSup_le hne fun x hx => ?_)
        Â· split_ifs <;> exact hlub.1 â€¹_â€º
        Â· refine (if_pos hx).symm.trans_le (le_bsup _ _ <| (hlub.1 hx).trans_lt (lt_succ _))
  tfae_have 5 â†’ 6
  Â· rintro âŸ¨o, hâ‚€, f, hfs, rflâŸ©
    exact âŸ¨_, out_nonempty_iff_ne_zero.2 hâ‚€, familyOfBFamily o f, fun _ => hfs _ _, rflâŸ©
  tfae_have 6 â†’ 1
  Â· rintro âŸ¨Î¹, hne, f, hfs, rflâŸ©
    rw [sup, iSup]
    exact closure_mono (range_subset_iff.2 hfs) <| csSup_mem_closure (range_nonempty f)
      (bddAbove_range.{u, u} f)
  tfae_finish

theorem mem_closure_iff_sup :
    a âˆˆ closure s â†”
      âˆƒ (Î¹ : Type u) (_ : Nonempty Î¹) (f : Î¹ â†’ Ordinal), (âˆ€ i, f i âˆˆ s) âˆ§ sup.{u, u} f = a :=
  ((mem_closure_tfae a s).out 0 5).trans <| by simp only [exists_prop]
#align ordinal.mem_closure_iff_sup Ordinal.mem_closure_iff_sup

theorem mem_closed_iff_sup (hs : IsClosed s) :
    a âˆˆ s â†” âˆƒ (Î¹ : Type u) (_hÎ¹ : Nonempty Î¹) (f : Î¹ â†’ Ordinal),
      (âˆ€ i, f i âˆˆ s) âˆ§ sup.{u, u} f = a := by
  rw [â† mem_closure_iff_sup, hs.closure_eq]
#align ordinal.mem_closed_iff_sup Ordinal.mem_closed_iff_sup

theorem mem_closure_iff_bsup :
    a âˆˆ closure s â†”
      âˆƒ (o : Ordinal) (_ho : o â‰  0) (f : âˆ€ a < o, Ordinal),
        (âˆ€ i hi, f i hi âˆˆ s) âˆ§ bsup.{u, u} o f = a :=
  ((mem_closure_tfae a s).out 0 4).trans <| by simp only [exists_prop]
#align ordinal.mem_closure_iff_bsup Ordinal.mem_closure_iff_bsup

theorem mem_closed_iff_bsup (hs : IsClosed s) :
    a âˆˆ s â†”
      âˆƒ (o : Ordinal) (_ho : o â‰  0) (f : âˆ€ a < o, Ordinal),
        (âˆ€ i hi, f i hi âˆˆ s) âˆ§ bsup.{u, u} o f = a := by
  rw [â† mem_closure_iff_bsup, hs.closure_eq]
#align ordinal.mem_closed_iff_bsup Ordinal.mem_closed_iff_bsup

theorem isClosed_iff_sup :
    IsClosed s â†”
      âˆ€ {Î¹ : Type u}, Nonempty Î¹ â†’ âˆ€ f : Î¹ â†’ Ordinal, (âˆ€ i, f i âˆˆ s) â†’ sup.{u, u} f âˆˆ s := by
  use fun hs Î¹ hÎ¹ f hf => (mem_closed_iff_sup hs).2 âŸ¨Î¹, hÎ¹, f, hf, rflâŸ©
  rw [â† closure_subset_iff_isClosed]
  intro h x hx
  rcases mem_closure_iff_sup.1 hx with âŸ¨Î¹, hÎ¹, f, hf, rflâŸ©
  exact h hÎ¹ f hf
#align ordinal.is_closed_iff_sup Ordinal.isClosed_iff_sup

theorem isClosed_iff_bsup :
    IsClosed s â†”
      âˆ€ {o : Ordinal}, o â‰  0 â†’ âˆ€ f : âˆ€ a < o, Ordinal,
        (âˆ€ i hi, f i hi âˆˆ s) â†’ bsup.{u, u} o f âˆˆ s := by
  rw [isClosed_iff_sup]
  refine âŸ¨fun H o ho f hf => H (out_nonempty_iff_ne_zero.2 ho) _ ?_, fun H Î¹ hÎ¹ f hf => ?_âŸ©
  Â· exact fun i => hf _ _
  Â· rw [â† bsup_eq_sup]
    apply H (type_ne_zero_iff_nonempty.2 hÎ¹)
    exact fun i hi => hf _
#align ordinal.is_closed_iff_bsup Ordinal.isClosed_iff_bsup

theorem isLimit_of_mem_frontier (ha : a âˆˆ frontier s) : IsLimit a := by
  simp only [frontier_eq_closure_inter_closure, Set.mem_inter_iff, mem_closure_iff] at ha
  by_contra h
  rw [â† isOpen_singleton_iff] at h
  rcases ha.1 _ h rfl with âŸ¨b, hb, hb'âŸ©
  rcases ha.2 _ h rfl with âŸ¨c, hc, hc'âŸ©
  rw [Set.mem_singleton_iff] at *
  subst hb; subst hc
  exact hc' hb'
#align ordinal.is_limit_of_mem_frontier Ordinal.isLimit_of_mem_frontier

theorem isNormal_iff_strictMono_and_continuous (f : Ordinal.{u} â†’ Ordinal.{u}) :
    IsNormal f â†” StrictMono f âˆ§ Continuous f := by
  refine âŸ¨fun h => âŸ¨h.strictMono, ?_âŸ©, ?_âŸ©
  Â· rw [continuous_def]
    intro s hs
    rw [isOpen_iff] at *
    intro o ho ho'
    rcases hs _ ho (h.isLimit ho') with âŸ¨a, ha, hasâŸ©
    rw [â† IsNormal.bsup_eq.{u, u} h ho', lt_bsup] at ha
    rcases ha with âŸ¨b, hb, habâŸ©
    exact
      âŸ¨b, hb, fun c hc =>
        Set.mem_preimage.2 (has âŸ¨hab.trans (h.strictMono hc.1), h.strictMono hc.2âŸ©)âŸ©
  Â· rw [isNormal_iff_strictMono_limit]
    rintro âŸ¨h, h'âŸ©
    refine âŸ¨h, fun o ho a h => ?_âŸ©
    suffices o âˆˆ f â»Â¹' Set.Iic a from Set.mem_preimage.1 this
    rw [mem_closed_iff_sup (IsClosed.preimage h' (@isClosed_Iic _ _ _ _ a))]
    exact
      âŸ¨_, out_nonempty_iff_ne_zero.2 ho.1, typein (Â· < Â·), fun i => h _ (typein_lt_self i),
        sup_typein_limit ho.2âŸ©
#align ordinal.is_normal_iff_strict_mono_and_continuous Ordinal.isNormal_iff_strictMono_and_continuous

theorem enumOrd_isNormal_iff_isClosed (hs : s.Unbounded (Â· < Â·)) :
    IsNormal (enumOrd s) â†” IsClosed s := by
  have Hs := enumOrd_strictMono hs
  refine
    âŸ¨fun h => isClosed_iff_sup.2 fun {Î¹} hÎ¹ f hf => ?_, fun h =>
      (isNormal_iff_strictMono_limit _).2 âŸ¨Hs, fun a ha o H => ?_âŸ©âŸ©
  Â· let g : Î¹ â†’ Ordinal.{u} := fun i => (enumOrdOrderIso hs).symm âŸ¨_, hf iâŸ©
    suffices enumOrd s (sup.{u, u} g) = sup.{u, u} f by
      rw [â† this]
      exact enumOrd_mem hs _
    rw [@IsNormal.sup.{u, u, u} _ h Î¹ g hÎ¹]
    congr
    ext x
    change ((enumOrdOrderIso hs) _).val = f x
    rw [OrderIso.apply_symm_apply]
  Â· rw [isClosed_iff_bsup] at h
    suffices enumOrd s a â‰¤ bsup.{u, u} a fun b (_ : b < a) => enumOrd s b from
      this.trans (bsup_le H)
    cases' enumOrd_surjective hs _
        (h ha.1 (fun b _ => enumOrd s b) fun b _ => enumOrd_mem hs b) with
      b hb
    rw [â† hb]
    apply Hs.monotone
    by_contra! hba
    apply (Hs (lt_succ b)).not_le
    rw [hb]
    exact le_bsup.{u, u} _ _ (ha.2 _ hba)
#align ordinal.enum_ord_is_normal_iff_is_closed Ordinal.enumOrd_isNormal_iff_isClosed

end Ordinal
