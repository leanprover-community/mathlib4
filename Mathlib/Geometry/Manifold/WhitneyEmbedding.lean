/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.FieldTheory.Finiteness
import Mathlib.Geometry.Manifold.Diffeomorph
import Mathlib.Geometry.Manifold.Instances.Real
import Mathlib.Geometry.Manifold.PartitionOfUnity

/-!
# Whitney embedding theorem

In this file we prove a version of the Whitney embedding theorem: for any compact real manifold `M`,
for sufficiently large `n` there exists a smooth embedding `M ‚Üí ‚Ñù^n`.

## TODO

* Prove the weak Whitney embedding theorem: any `œÉ`-compact smooth `m`-dimensional manifold can be
  embedded into `‚Ñù^(2m+1)`. This requires a version of Sard's theorem: for a locally Lipschitz
  continuous map `f : ‚Ñù^m ‚Üí ‚Ñù^n`, `m < n`, the range has Hausdorff dimension at most `m`, hence it
  has measure zero.

## Tags

partition of unity, smooth bump function, whitney theorem
-/

universe uŒπ uE uH uM

open Function Filter Module Set Topology
open scoped Manifold ContDiff

variable {Œπ : Type uŒπ} {E : Type uE} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
  [FiniteDimensional ‚Ñù E] {H : Type uH} [TopologicalSpace H] {I : ModelWithCorners ‚Ñù E H}
  {M : Type uM} [TopologicalSpace M] [ChartedSpace H M] [IsManifold I ‚àû M]


noncomputable section

namespace SmoothBumpCovering

/-!
### Whitney embedding theorem

In this section we prove a version of the Whitney embedding theorem: for any compact real manifold
`M`, for sufficiently large `n` there exists a smooth embedding `M ‚Üí ‚Ñù^n`.
-/

variable [T2Space M] [Fintype Œπ] {s : Set M} (f : SmoothBumpCovering Œπ I M s)

/-- Smooth embedding of `M` into `(E √ó ‚Ñù) ^ Œπ`. -/
def embeddingPiTangent : C^‚àû‚üÆI, M; ùìò(‚Ñù, Œπ ‚Üí E √ó ‚Ñù), Œπ ‚Üí E √ó ‚Ñù‚üØ where
  val x i := (f i x ‚Ä¢ extChartAt I (f.c i) x, f i x)
  property :=
    contMDiff_pi_space.2 fun i ‚Ü¶
      ((f i).contMDiff_smul contMDiffOn_extChartAt).prodMk_space (f i).contMDiff

@[local simp]
theorem embeddingPiTangent_coe :
    ‚áëf.embeddingPiTangent = fun x i ‚Ü¶ (f i x ‚Ä¢ extChartAt I (f.c i) x, f i x) :=
  rfl

theorem embeddingPiTangent_injOn : InjOn f.embeddingPiTangent s := by
  intro x hx y _ h
  simp only [embeddingPiTangent_coe, funext_iff] at h
  obtain ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© := Prod.mk_inj.1 (h (f.ind x hx))
  rw [f.apply_ind x hx] at h‚ÇÇ
  rw [‚Üê h‚ÇÇ, f.apply_ind x hx, one_smul, one_smul] at h‚ÇÅ
  have := f.mem_extChartAt_source_of_eq_one h‚ÇÇ.symm
  exact (extChartAt I (f.c _)).injOn (f.mem_extChartAt_ind_source x hx) this h‚ÇÅ

theorem embeddingPiTangent_injective (f : SmoothBumpCovering Œπ I M) :
    Injective f.embeddingPiTangent :=
  injective_iff_injOn_univ.2 f.embeddingPiTangent_injOn

theorem comp_embeddingPiTangent_mfderiv (x : M) (hx : x ‚àà s) :
    ((ContinuousLinearMap.fst ‚Ñù E ‚Ñù).comp
            (@ContinuousLinearMap.proj ‚Ñù _ Œπ (fun _ ‚Ü¶ E √ó ‚Ñù) _ _ (fun _ ‚Ü¶ inferInstance)
              (f.ind x hx))).comp
        (mfderiv I ùìò(‚Ñù, Œπ ‚Üí E √ó ‚Ñù) f.embeddingPiTangent x) =
      mfderiv I I (chartAt H (f.c (f.ind x hx))) x := by
  set L :=
    (ContinuousLinearMap.fst ‚Ñù E ‚Ñù).comp
      (@ContinuousLinearMap.proj ‚Ñù _ Œπ (fun _ ‚Ü¶ E √ó ‚Ñù) _ _ (fun _ ‚Ü¶ inferInstance) (f.ind x hx))
  have := L.hasMFDerivAt.comp x
    (f.embeddingPiTangent.contMDiff.mdifferentiableAt (mod_cast le_top)).hasMFDerivAt
  convert hasMFDerivAt_unique this _
  refine (hasMFDerivAt_extChartAt (f.mem_chartAt_ind_source x hx)).congr_of_eventuallyEq ?_
  refine (f.eventuallyEq_one x hx).mono fun y hy ‚Ü¶ ?_
  simp only [L, embeddingPiTangent_coe, ContinuousLinearMap.coe_comp', (¬∑ ‚àò ¬∑),
    ContinuousLinearMap.coe_fst', ContinuousLinearMap.proj_apply]
  rw [hy, Pi.one_apply, one_smul]

theorem embeddingPiTangent_ker_mfderiv (x : M) (hx : x ‚àà s) :
    LinearMap.ker (mfderiv I ùìò(‚Ñù, Œπ ‚Üí E √ó ‚Ñù) f.embeddingPiTangent x) = ‚ä• := by
  apply bot_unique
  rw [‚Üê (mdifferentiable_chart (f.c (f.ind x hx))).ker_mfderiv_eq_bot
      (f.mem_chartAt_ind_source x hx),
    ‚Üê comp_embeddingPiTangent_mfderiv]
  exact LinearMap.ker_le_ker_comp _ _

theorem embeddingPiTangent_injective_mfderiv (x : M) (hx : x ‚àà s) :
    Injective (mfderiv I ùìò(‚Ñù, Œπ ‚Üí E √ó ‚Ñù) f.embeddingPiTangent x) :=
  LinearMap.ker_eq_bot.1 (f.embeddingPiTangent_ker_mfderiv x hx)

/-- Baby version of the **Whitney weak embedding theorem**: if `M` admits a finite covering by
supports of bump functions, then for some `n` it can be immersed into the `n`-dimensional
Euclidean space. -/
theorem exists_immersion_euclidean {Œπ : Type*} [Finite Œπ] (f : SmoothBumpCovering Œπ I M) :
    ‚àÉ (n : ‚Ñï) (e : M ‚Üí EuclideanSpace ‚Ñù (Fin n)),
      ContMDiff I (ùì° n) ‚àû e ‚àß Injective e ‚àß ‚àÄ x : M, Injective (mfderiv I (ùì° n) e x) := by
  cases nonempty_fintype Œπ
  set F := EuclideanSpace ‚Ñù (Fin <| finrank ‚Ñù (Œπ ‚Üí E √ó ‚Ñù))
  letI : IsNoetherian ‚Ñù (E √ó ‚Ñù) := IsNoetherian.iff_fg.2 inferInstance
  letI : FiniteDimensional ‚Ñù (Œπ ‚Üí E √ó ‚Ñù) := IsNoetherian.iff_fg.1 inferInstance
  set eEF : (Œπ ‚Üí E √ó ‚Ñù) ‚âÉL[‚Ñù] F :=
    ContinuousLinearEquiv.ofFinrankEq finrank_euclideanSpace_fin.symm
  refine ‚ü®_, eEF ‚àò f.embeddingPiTangent,
    eEF.toDiffeomorph.contMDiff.comp f.embeddingPiTangent.contMDiff,
    eEF.injective.comp f.embeddingPiTangent_injective, fun x ‚Ü¶ ?_‚ü©
  rw [mfderiv_comp _ eEF.differentiableAt.mdifferentiableAt
      (f.embeddingPiTangent.contMDiff.mdifferentiableAt (mod_cast le_top)),
    eEF.mfderiv_eq]
  exact eEF.injective.comp (f.embeddingPiTangent_injective_mfderiv _ trivial)

end SmoothBumpCovering

/-- Baby version of the Whitney weak embedding theorem: if `M` admits a finite covering by
supports of bump functions, then for some `n` it can be embedded into the `n`-dimensional
Euclidean space. -/
theorem exists_embedding_euclidean_of_compact [T2Space M] [CompactSpace M] :
    ‚àÉ (n : ‚Ñï) (e : M ‚Üí EuclideanSpace ‚Ñù (Fin n)),
      ContMDiff I (ùì° n) ‚àû e ‚àß IsClosedEmbedding e ‚àß ‚àÄ x : M, Injective (mfderiv I (ùì° n) e x) := by
  rcases SmoothBumpCovering.exists_isSubordinate I isClosed_univ fun (x : M) _ ‚Ü¶ univ_mem with
    ‚ü®Œπ, f, -‚ü©
  haveI := f.fintype
  rcases f.exists_immersion_euclidean with ‚ü®n, e, hsmooth, hinj, hinj_mfderiv‚ü©
  exact ‚ü®n, e, hsmooth, hsmooth.continuous.isClosedEmbedding hinj, hinj_mfderiv‚ü©
