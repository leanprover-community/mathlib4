/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Floris van Doorn
-/
import Mathlib.Geometry.Manifold.MFDeriv.Basic

/-!
### Relations between vector space derivative and manifold derivative

The manifold derivative `mfderiv`, when considered on the model vector space with its trivial
manifold structure, coincides with the usual FrÃ©chet derivative `fderiv`. In this section, we prove
this and related statements.
-/

noncomputable section

open scoped Manifold

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E'] {f : E â†’ E'}
  {s : Set E} {x : E}

section MFDerivFDeriv

theorem uniqueMDiffWithinAt_iff_uniqueDiffWithinAt :
    UniqueMDiffWithinAt ğ“˜(ğ•œ, E) s x â†” UniqueDiffWithinAt ğ•œ s x := by
  simp only [UniqueMDiffWithinAt, mfld_simps]

alias âŸ¨UniqueMDiffWithinAt.uniqueDiffWithinAt, UniqueDiffWithinAt.uniqueMDiffWithinAtâŸ© :=
  uniqueMDiffWithinAt_iff_uniqueDiffWithinAt

theorem uniqueMDiffOn_iff_uniqueDiffOn : UniqueMDiffOn ğ“˜(ğ•œ, E) s â†” UniqueDiffOn ğ•œ s := by
  simp [UniqueMDiffOn, UniqueDiffOn, uniqueMDiffWithinAt_iff_uniqueDiffWithinAt]

alias âŸ¨UniqueMDiffOn.uniqueDiffOn, UniqueDiffOn.uniqueMDiffOnâŸ© := uniqueMDiffOn_iff_uniqueDiffOn

theorem ModelWithCorners.uniqueMDiffOn {H : Type*} [TopologicalSpace H]
    (I : ModelWithCorners ğ•œ E H) : UniqueMDiffOn ğ“˜(ğ•œ, E) (Set.range I) :=
  I.uniqueDiffOn.uniqueMDiffOn

@[simp, mfld_simps]
theorem writtenInExtChartAt_model_space : writtenInExtChartAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') x f = f :=
  rfl

theorem hasMFDerivWithinAt_iff_hasFDerivWithinAt {f'} :
    HasMFDerivWithinAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s x f' â†” HasFDerivWithinAt f f' s x := by
  simpa only [HasMFDerivWithinAt, and_iff_right_iff_imp, mfld_simps] using
    HasFDerivWithinAt.continuousWithinAt

alias âŸ¨HasMFDerivWithinAt.hasFDerivWithinAt, HasFDerivWithinAt.hasMFDerivWithinAtâŸ© :=
  hasMFDerivWithinAt_iff_hasFDerivWithinAt

theorem hasMFDerivAt_iff_hasFDerivAt {f'} :
    HasMFDerivAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f x f' â†” HasFDerivAt f f' x := by
  rw [â† hasMFDerivWithinAt_univ, hasMFDerivWithinAt_iff_hasFDerivWithinAt, hasFDerivWithinAt_univ]

alias âŸ¨HasMFDerivAt.hasFDerivAt, HasFDerivAt.hasMFDerivAtâŸ© := hasMFDerivAt_iff_hasFDerivAt

/-- For maps between vector spaces, `MDifferentiableWithinAt` and `DifferentiableWithinAt`
coincide -/
theorem mdifferentiableWithinAt_iff_differentiableWithinAt :
    MDifferentiableWithinAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s x â†” DifferentiableWithinAt ğ•œ f s x := by
  simp only [mdifferentiableWithinAt_iff', mfld_simps]
  exact âŸ¨fun H => H.2, fun H => âŸ¨H.continuousWithinAt, HâŸ©âŸ©

alias âŸ¨MDifferentiableWithinAt.differentiableWithinAt,
    DifferentiableWithinAt.mdifferentiableWithinAtâŸ© :=
  mdifferentiableWithinAt_iff_differentiableWithinAt

/-- For maps between vector spaces, `MDifferentiableAt` and `DifferentiableAt` coincide -/
theorem mdifferentiableAt_iff_differentiableAt :
    MDifferentiableAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f x â†” DifferentiableAt ğ•œ f x := by
  simp only [mdifferentiableAt_iff, differentiableWithinAt_univ, mfld_simps]
  exact âŸ¨fun H => H.2, fun H => âŸ¨H.continuousAt, HâŸ©âŸ©

alias âŸ¨MDifferentiableAt.differentiableAt, DifferentiableAt.mdifferentiableAtâŸ© :=
  mdifferentiableAt_iff_differentiableAt

/-- For maps between vector spaces, `MDifferentiableOn` and `DifferentiableOn` coincide -/
theorem mdifferentiableOn_iff_differentiableOn :
    MDifferentiableOn ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s â†” DifferentiableOn ğ•œ f s := by
  simp only [MDifferentiableOn, DifferentiableOn,
    mdifferentiableWithinAt_iff_differentiableWithinAt]

alias âŸ¨MDifferentiableOn.differentiableOn, DifferentiableOn.mdifferentiableOnâŸ© :=
  mdifferentiableOn_iff_differentiableOn

/-- For maps between vector spaces, `MDifferentiable` and `Differentiable` coincide -/
theorem mdifferentiable_iff_differentiable :
    MDifferentiable ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f â†” Differentiable ğ•œ f := by
  simp only [MDifferentiable, Differentiable, mdifferentiableAt_iff_differentiableAt]

alias âŸ¨MDifferentiable.differentiable, Differentiable.mdifferentiableâŸ© :=
  mdifferentiable_iff_differentiable

/-- For maps between vector spaces, `mfderivWithin` and `fderivWithin` coincide -/
@[simp]
theorem mfderivWithin_eq_fderivWithin :
    mfderivWithin ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s x = fderivWithin ğ•œ f s x := by
  by_cases h : MDifferentiableWithinAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s x
  Â· simp only [mfderivWithin, h, if_pos, mfld_simps]
  Â· simp only [mfderivWithin, h, if_neg, not_false_iff]
    rw [mdifferentiableWithinAt_iff_differentiableWithinAt] at h
    exact (fderivWithin_zero_of_not_differentiableWithinAt h).symm

/-- For maps between vector spaces, `mfderiv` and `fderiv` coincide -/
@[simp]
theorem mfderiv_eq_fderiv : mfderiv ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f x = fderiv ğ•œ f x := by
  rw [â† mfderivWithin_univ, â† fderivWithin_univ]
  exact mfderivWithin_eq_fderivWithin

end MFDerivFDeriv
