/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Floris van Doorn
-/
import Mathlib.Geometry.Manifold.MFDeriv.FDeriv

/-!
# Differentiability of specific functions

In this file, we establish differentiability results for
- continuous linear maps and continuous linear equivalences
- the identity
- constant functions
- products
- arithmetic operations (such as addition and scalar multiplication).

-/

noncomputable section

open scoped Manifold
open Bundle Set Topology

section SpecificFunctions

/-! ### Differentiability of specific functions -/

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H) {M : Type*}
  [TopologicalSpace M] [ChartedSpace H M] [SmoothManifoldWithCorners I M] {E' : Type*}
  [NormedAddCommGroup E'] [NormedSpace ğ•œ E'] {H' : Type*} [TopologicalSpace H']
  (I' : ModelWithCorners ğ•œ E' H') {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M']
  [SmoothManifoldWithCorners I' M'] {E'' : Type*} [NormedAddCommGroup E''] [NormedSpace ğ•œ E'']
  {H'' : Type*} [TopologicalSpace H''] (I'' : ModelWithCorners ğ•œ E'' H'') {M'' : Type*}
  [TopologicalSpace M''] [ChartedSpace H'' M''] [SmoothManifoldWithCorners I'' M'']

namespace ContinuousLinearMap

variable (f : E â†’L[ğ•œ] E') {s : Set E} {x : E}

protected theorem hasMFDerivWithinAt : HasMFDerivWithinAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s x f :=
  f.hasFDerivWithinAt.hasMFDerivWithinAt

protected theorem hasMFDerivAt : HasMFDerivAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f x f :=
  f.hasFDerivAt.hasMFDerivAt

protected theorem mdifferentiableWithinAt : MDifferentiableWithinAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s x :=
  f.differentiableWithinAt.mdifferentiableWithinAt

protected theorem mdifferentiableOn : MDifferentiableOn ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s :=
  f.differentiableOn.mdifferentiableOn

protected theorem mdifferentiableAt : MDifferentiableAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f x :=
  f.differentiableAt.mdifferentiableAt

protected theorem mdifferentiable : MDifferentiable ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f :=
  f.differentiable.mdifferentiable

theorem mfderiv_eq : mfderiv ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f x = f :=
  f.hasMFDerivAt.mfderiv

theorem mfderivWithin_eq (hs : UniqueMDiffWithinAt ğ“˜(ğ•œ, E) s x) :
    mfderivWithin ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s x = f :=
  f.hasMFDerivWithinAt.mfderivWithin hs

end ContinuousLinearMap

namespace ContinuousLinearEquiv

variable (f : E â‰ƒL[ğ•œ] E') {s : Set E} {x : E}

protected theorem hasMFDerivWithinAt : HasMFDerivWithinAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s x (f : E â†’L[ğ•œ] E') :=
  f.hasFDerivWithinAt.hasMFDerivWithinAt

protected theorem hasMFDerivAt : HasMFDerivAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f x (f : E â†’L[ğ•œ] E') :=
  f.hasFDerivAt.hasMFDerivAt

protected theorem mdifferentiableWithinAt : MDifferentiableWithinAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s x :=
  f.differentiableWithinAt.mdifferentiableWithinAt

protected theorem mdifferentiableOn : MDifferentiableOn ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s :=
  f.differentiableOn.mdifferentiableOn

protected theorem mdifferentiableAt : MDifferentiableAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f x :=
  f.differentiableAt.mdifferentiableAt

protected theorem mdifferentiable : MDifferentiable ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f :=
  f.differentiable.mdifferentiable

theorem mfderiv_eq : mfderiv ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f x = (f : E â†’L[ğ•œ] E') :=
  f.hasMFDerivAt.mfderiv

theorem mfderivWithin_eq (hs : UniqueMDiffWithinAt ğ“˜(ğ•œ, E) s x) :
    mfderivWithin ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s x = (f : E â†’L[ğ•œ] E') :=
  f.hasMFDerivWithinAt.mfderivWithin hs

end ContinuousLinearEquiv

variable {s : Set M} {x : M}

section id

/-! #### Identity -/

theorem hasMFDerivAt_id (x : M) :
    HasMFDerivAt I I (@id M) x (ContinuousLinearMap.id ğ•œ (TangentSpace I x)) := by
  refine âŸ¨continuousAt_id, ?_âŸ©
  have : âˆ€á¶  y in ğ“[range I] (extChartAt I x) x, (extChartAt I x âˆ˜ (extChartAt I x).symm) y = y := by
    apply Filter.mem_of_superset (extChartAt_target_mem_nhdsWithin I x)
    mfld_set_tac
  apply HasFDerivWithinAt.congr_of_eventuallyEq (hasFDerivWithinAt_id _ _) this
  simp only [mfld_simps]

theorem hasMFDerivWithinAt_id (s : Set M) (x : M) :
    HasMFDerivWithinAt I I (@id M) s x (ContinuousLinearMap.id ğ•œ (TangentSpace I x)) :=
  (hasMFDerivAt_id I x).hasMFDerivWithinAt

theorem mdifferentiableAt_id : MDifferentiableAt I I (@id M) x :=
  (hasMFDerivAt_id I x).mdifferentiableAt

theorem mdifferentiableWithinAt_id : MDifferentiableWithinAt I I (@id M) s x :=
  (mdifferentiableAt_id I).mdifferentiableWithinAt

theorem mdifferentiable_id : MDifferentiable I I (@id M) := fun _ => mdifferentiableAt_id I

theorem mdifferentiableOn_id : MDifferentiableOn I I (@id M) s :=
  (mdifferentiable_id I).mdifferentiableOn

@[simp, mfld_simps]
theorem mfderiv_id : mfderiv I I (@id M) x = ContinuousLinearMap.id ğ•œ (TangentSpace I x) :=
  HasMFDerivAt.mfderiv (hasMFDerivAt_id I x)

theorem mfderivWithin_id (hxs : UniqueMDiffWithinAt I s x) :
    mfderivWithin I I (@id M) s x = ContinuousLinearMap.id ğ•œ (TangentSpace I x) := by
  rw [MDifferentiable.mfderivWithin (mdifferentiableAt_id I) hxs]
  exact mfderiv_id I

@[simp, mfld_simps]
theorem tangentMap_id : tangentMap I I (id : M â†’ M) = id := by ext1 âŸ¨x, vâŸ©; simp [tangentMap]

theorem tangentMapWithin_id {p : TangentBundle I M} (hs : UniqueMDiffWithinAt I s p.proj) :
    tangentMapWithin I I (id : M â†’ M) s p = p := by
  simp only [tangentMapWithin, id]
  rw [mfderivWithin_id]
  Â· rcases p with âŸ¨âŸ©; rfl
  Â· exact hs

end id

section Const

/-! #### Constants -/


variable {c : M'}

theorem hasMFDerivAt_const (c : M') (x : M) :
    HasMFDerivAt I I' (fun _ : M => c) x (0 : TangentSpace I x â†’L[ğ•œ] TangentSpace I' c) := by
  refine âŸ¨continuous_const.continuousAt, ?_âŸ©
  simp only [writtenInExtChartAt, Function.comp_def, hasFDerivWithinAt_const]

theorem hasMFDerivWithinAt_const (c : M') (s : Set M) (x : M) :
    HasMFDerivWithinAt I I' (fun _ : M => c) s x (0 : TangentSpace I x â†’L[ğ•œ] TangentSpace I' c) :=
  (hasMFDerivAt_const I I' c x).hasMFDerivWithinAt

theorem mdifferentiableAt_const : MDifferentiableAt I I' (fun _ : M => c) x :=
  (hasMFDerivAt_const I I' c x).mdifferentiableAt

theorem mdifferentiableWithinAt_const : MDifferentiableWithinAt I I' (fun _ : M => c) s x :=
  (mdifferentiableAt_const I I').mdifferentiableWithinAt

theorem mdifferentiable_const : MDifferentiable I I' fun _ : M => c := fun _ =>
  mdifferentiableAt_const I I'

theorem mdifferentiableOn_const : MDifferentiableOn I I' (fun _ : M => c) s :=
  (mdifferentiable_const I I').mdifferentiableOn

@[simp, mfld_simps]
theorem mfderiv_const :
    mfderiv I I' (fun _ : M => c) x = (0 : TangentSpace I x â†’L[ğ•œ] TangentSpace I' c) :=
  HasMFDerivAt.mfderiv (hasMFDerivAt_const I I' c x)

theorem mfderivWithin_const (hxs : UniqueMDiffWithinAt I s x) :
    mfderivWithin I I' (fun _ : M => c) s x = (0 : TangentSpace I x â†’L[ğ•œ] TangentSpace I' c) :=
  (hasMFDerivWithinAt_const _ _ _ _ _).mfderivWithin hxs

end Const

section Prod

/-! ### Operations on the product of two manifolds -/

theorem hasMFDerivAt_fst (x : M Ã— M') :
    HasMFDerivAt (I.prod I') I Prod.fst x
      (ContinuousLinearMap.fst ğ•œ (TangentSpace I x.1) (TangentSpace I' x.2)) := by
  refine âŸ¨continuous_fst.continuousAt, ?_âŸ©
  have :
    âˆ€á¶  y in ğ“[range (I.prod I')] extChartAt (I.prod I') x x,
      (extChartAt I x.1 âˆ˜ Prod.fst âˆ˜ (extChartAt (I.prod I') x).symm) y = y.1 := by
    /- porting note: was
    apply Filter.mem_of_superset (extChartAt_target_mem_nhdsWithin (I.prod I') x)
    mfld_set_tac
    -/
    filter_upwards [extChartAt_target_mem_nhdsWithin (I.prod I') x] with y hy
    rw [extChartAt_prod] at hy
    exact (extChartAt I x.1).right_inv hy.1
  apply HasFDerivWithinAt.congr_of_eventuallyEq hasFDerivWithinAt_fst this
  -- Porting note: next line was `simp only [mfld_simps]`
  exact (extChartAt I x.1).right_inv <| (extChartAt I x.1).map_source (mem_extChartAt_source _ _)

theorem hasMFDerivWithinAt_fst (s : Set (M Ã— M')) (x : M Ã— M') :
    HasMFDerivWithinAt (I.prod I') I Prod.fst s x
      (ContinuousLinearMap.fst ğ•œ (TangentSpace I x.1) (TangentSpace I' x.2)) :=
  (hasMFDerivAt_fst I I' x).hasMFDerivWithinAt

theorem mdifferentiableAt_fst {x : M Ã— M'} : MDifferentiableAt (I.prod I') I Prod.fst x :=
  (hasMFDerivAt_fst I I' x).mdifferentiableAt

theorem mdifferentiableWithinAt_fst {s : Set (M Ã— M')} {x : M Ã— M'} :
    MDifferentiableWithinAt (I.prod I') I Prod.fst s x :=
  (mdifferentiableAt_fst I I').mdifferentiableWithinAt

theorem mdifferentiable_fst : MDifferentiable (I.prod I') I (Prod.fst : M Ã— M' â†’ M) := fun _ =>
  mdifferentiableAt_fst I I'

theorem mdifferentiableOn_fst {s : Set (M Ã— M')} : MDifferentiableOn (I.prod I') I Prod.fst s :=
  (mdifferentiable_fst I I').mdifferentiableOn

@[simp, mfld_simps]
theorem mfderiv_fst {x : M Ã— M'} :
    mfderiv (I.prod I') I Prod.fst x =
      ContinuousLinearMap.fst ğ•œ (TangentSpace I x.1) (TangentSpace I' x.2) :=
  (hasMFDerivAt_fst I I' x).mfderiv

theorem mfderivWithin_fst {s : Set (M Ã— M')} {x : M Ã— M'}
    (hxs : UniqueMDiffWithinAt (I.prod I') s x) :
    mfderivWithin (I.prod I') I Prod.fst s x =
      ContinuousLinearMap.fst ğ•œ (TangentSpace I x.1) (TangentSpace I' x.2) := by
  rw [MDifferentiable.mfderivWithin (mdifferentiableAt_fst I I') hxs]; exact mfderiv_fst I I'

@[simp, mfld_simps]
theorem tangentMap_prod_fst {p : TangentBundle (I.prod I') (M Ã— M')} :
    tangentMap (I.prod I') I Prod.fst p = âŸ¨p.proj.1, p.2.1âŸ© := by
  -- Porting note: `rfl` wasn't needed
  simp [tangentMap]; rfl

theorem tangentMapWithin_prod_fst {s : Set (M Ã— M')} {p : TangentBundle (I.prod I') (M Ã— M')}
    (hs : UniqueMDiffWithinAt (I.prod I') s p.proj) :
    tangentMapWithin (I.prod I') I Prod.fst s p = âŸ¨p.proj.1, p.2.1âŸ© := by
  simp only [tangentMapWithin]
  rw [mfderivWithin_fst]
  Â· rcases p with âŸ¨âŸ©; rfl
  Â· exact hs

theorem hasMFDerivAt_snd (x : M Ã— M') :
    HasMFDerivAt (I.prod I') I' Prod.snd x
      (ContinuousLinearMap.snd ğ•œ (TangentSpace I x.1) (TangentSpace I' x.2)) := by
  refine âŸ¨continuous_snd.continuousAt, ?_âŸ©
  have :
    âˆ€á¶  y in ğ“[range (I.prod I')] extChartAt (I.prod I') x x,
      (extChartAt I' x.2 âˆ˜ Prod.snd âˆ˜ (extChartAt (I.prod I') x).symm) y = y.2 := by
    /- porting note: was
    apply Filter.mem_of_superset (extChartAt_target_mem_nhdsWithin (I.prod I') x)
    mfld_set_tac
    -/
    filter_upwards [extChartAt_target_mem_nhdsWithin (I.prod I') x] with y hy
    rw [extChartAt_prod] at hy
    exact (extChartAt I' x.2).right_inv hy.2
  apply HasFDerivWithinAt.congr_of_eventuallyEq hasFDerivWithinAt_snd this
  -- Porting note: the next line was `simp only [mfld_simps]`
  exact (extChartAt I' x.2).right_inv <| (extChartAt I' x.2).map_source (mem_extChartAt_source _ _)

theorem hasMFDerivWithinAt_snd (s : Set (M Ã— M')) (x : M Ã— M') :
    HasMFDerivWithinAt (I.prod I') I' Prod.snd s x
      (ContinuousLinearMap.snd ğ•œ (TangentSpace I x.1) (TangentSpace I' x.2)) :=
  (hasMFDerivAt_snd I I' x).hasMFDerivWithinAt

theorem mdifferentiableAt_snd {x : M Ã— M'} : MDifferentiableAt (I.prod I') I' Prod.snd x :=
  (hasMFDerivAt_snd I I' x).mdifferentiableAt

theorem mdifferentiableWithinAt_snd {s : Set (M Ã— M')} {x : M Ã— M'} :
    MDifferentiableWithinAt (I.prod I') I' Prod.snd s x :=
  (mdifferentiableAt_snd I I').mdifferentiableWithinAt

theorem mdifferentiable_snd : MDifferentiable (I.prod I') I' (Prod.snd : M Ã— M' â†’ M') := fun _ =>
  mdifferentiableAt_snd I I'

theorem mdifferentiableOn_snd {s : Set (M Ã— M')} : MDifferentiableOn (I.prod I') I' Prod.snd s :=
  (mdifferentiable_snd I I').mdifferentiableOn

@[simp, mfld_simps]
theorem mfderiv_snd {x : M Ã— M'} :
    mfderiv (I.prod I') I' Prod.snd x =
      ContinuousLinearMap.snd ğ•œ (TangentSpace I x.1) (TangentSpace I' x.2) :=
  (hasMFDerivAt_snd I I' x).mfderiv

theorem mfderivWithin_snd {s : Set (M Ã— M')} {x : M Ã— M'}
    (hxs : UniqueMDiffWithinAt (I.prod I') s x) :
    mfderivWithin (I.prod I') I' Prod.snd s x =
      ContinuousLinearMap.snd ğ•œ (TangentSpace I x.1) (TangentSpace I' x.2) := by
  rw [MDifferentiable.mfderivWithin (mdifferentiableAt_snd I I') hxs]; exact mfderiv_snd I I'

@[simp, mfld_simps]
theorem tangentMap_prod_snd {p : TangentBundle (I.prod I') (M Ã— M')} :
    tangentMap (I.prod I') I' Prod.snd p = âŸ¨p.proj.2, p.2.2âŸ© := by
  -- Porting note: `rfl` wasn't needed
  simp [tangentMap]; rfl

theorem tangentMapWithin_prod_snd {s : Set (M Ã— M')} {p : TangentBundle (I.prod I') (M Ã— M')}
    (hs : UniqueMDiffWithinAt (I.prod I') s p.proj) :
    tangentMapWithin (I.prod I') I' Prod.snd s p = âŸ¨p.proj.2, p.2.2âŸ© := by
  simp only [tangentMapWithin]
  rw [mfderivWithin_snd]
  Â· rcases p with âŸ¨âŸ©; rfl
  Â· exact hs

variable {I I' I''}

theorem MDifferentiableAt.mfderiv_prod {f : M â†’ M'} {g : M â†’ M''} {x : M}
    (hf : MDifferentiableAt I I' f x) (hg : MDifferentiableAt I I'' g x) :
    mfderiv I (I'.prod I'') (fun x => (f x, g x)) x =
      (mfderiv I I' f x).prod (mfderiv I I'' g x) := by
  classical
  simp_rw [mfderiv, if_pos (hf.prod_mk hg), if_pos hf, if_pos hg]
  exact hf.differentiableWithinAt_writtenInExtChartAt.fderivWithin_prod
    hg.differentiableWithinAt_writtenInExtChartAt (I.unique_diff _ (mem_range_self _))

variable (I I' I'')

theorem mfderiv_prod_left {xâ‚€ : M} {yâ‚€ : M'} :
    mfderiv I (I.prod I') (fun x => (x, yâ‚€)) xâ‚€ =
      ContinuousLinearMap.inl ğ•œ (TangentSpace I xâ‚€) (TangentSpace I' yâ‚€) := by
  refine ((mdifferentiableAt_id I).mfderiv_prod (mdifferentiableAt_const I I')).trans ?_
  rw [mfderiv_id, mfderiv_const, ContinuousLinearMap.inl]

theorem mfderiv_prod_right {xâ‚€ : M} {yâ‚€ : M'} :
    mfderiv I' (I.prod I') (fun y => (xâ‚€, y)) yâ‚€ =
      ContinuousLinearMap.inr ğ•œ (TangentSpace I xâ‚€) (TangentSpace I' yâ‚€) := by
  refine ((mdifferentiableAt_const I' I).mfderiv_prod (mdifferentiableAt_id I')).trans ?_
  rw [mfderiv_id, mfderiv_const, ContinuousLinearMap.inr]

/-- The total derivative of a function in two variables is the sum of the partial derivatives.
  Note that to state this (without casts) we need to be able to see through the definition of
  `TangentSpace`. -/
theorem mfderiv_prod_eq_add {f : M Ã— M' â†’ M''} {p : M Ã— M'}
    (hf : MDifferentiableAt (I.prod I') I'' f p) :
    mfderiv (I.prod I') I'' f p =
      show E Ã— E' â†’L[ğ•œ] E'' from
        mfderiv (I.prod I') I'' (fun z : M Ã— M' => f (z.1, p.2)) p +
          mfderiv (I.prod I') I'' (fun z : M Ã— M' => f (p.1, z.2)) p := by
  dsimp only
  erw [mfderiv_comp_of_eq hf ((mdifferentiableAt_fst I I').prod_mk (mdifferentiableAt_const _ _))
      rfl,
    mfderiv_comp_of_eq hf ((mdifferentiableAt_const _ _).prod_mk (mdifferentiableAt_snd I I')) rfl,
    â† ContinuousLinearMap.comp_add,
    (mdifferentiableAt_fst I I').mfderiv_prod (mdifferentiableAt_const (I.prod I') I'),
    (mdifferentiableAt_const (I.prod I') I).mfderiv_prod (mdifferentiableAt_snd I I'), mfderiv_fst,
    mfderiv_snd, mfderiv_const, mfderiv_const]
  symm
  convert ContinuousLinearMap.comp_id <| mfderiv (.prod I I') I'' f (p.1, p.2)
  exact ContinuousLinearMap.coprod_inl_inr

end Prod

section Arithmetic

/-! #### Arithmetic

Note that in the `HasMFDerivAt` lemmas there is an abuse of the defeq between `E'` and
`TangentSpace ğ“˜(ğ•œ, E') (f z)` (similarly for `g',F',p',q'`). In general this defeq is not
canonical, but in this case (the tangent space of a vector space) it is canonical.
 -/

section Group

variable {I}
variable {z : M} {f g : M â†’ E'} {f' g' : TangentSpace I z â†’L[ğ•œ] E'}

theorem HasMFDerivAt.add (hf : HasMFDerivAt I ğ“˜(ğ•œ, E') f z f')
    (hg : HasMFDerivAt I ğ“˜(ğ•œ, E') g z g') : HasMFDerivAt I ğ“˜(ğ•œ, E') (f + g) z (f' + g') :=
  âŸ¨hf.1.add hg.1, hf.2.add hg.2âŸ©

theorem MDifferentiableAt.add (hf : MDifferentiableAt I ğ“˜(ğ•œ, E') f z)
    (hg : MDifferentiableAt I ğ“˜(ğ•œ, E') g z) : MDifferentiableAt I ğ“˜(ğ•œ, E') (f + g) z :=
  (hf.hasMFDerivAt.add hg.hasMFDerivAt).mdifferentiableAt

theorem MDifferentiable.add (hf : MDifferentiable I ğ“˜(ğ•œ, E') f)
    (hg : MDifferentiable I ğ“˜(ğ•œ, E') g) : MDifferentiable I ğ“˜(ğ•œ, E') (f + g) := fun x =>
  (hf x).add (hg x)

-- Porting note: forcing types using `by exact`
theorem mfderiv_add (hf : MDifferentiableAt I ğ“˜(ğ•œ, E') f z)
    (hg : MDifferentiableAt I ğ“˜(ğ•œ, E') g z) :
    (by exact mfderiv I ğ“˜(ğ•œ, E') (f + g) z : TangentSpace I z â†’L[ğ•œ] E') =
      (by exact mfderiv I ğ“˜(ğ•œ, E') f z) + (by exact mfderiv I ğ“˜(ğ•œ, E') g z) :=
  (hf.hasMFDerivAt.add hg.hasMFDerivAt).mfderiv

theorem HasMFDerivAt.const_smul (hf : HasMFDerivAt I ğ“˜(ğ•œ, E') f z f') (s : ğ•œ) :
    HasMFDerivAt I ğ“˜(ğ•œ, E') (s â€¢ f) z (s â€¢ f') :=
  âŸ¨hf.1.const_smul s, hf.2.const_smul sâŸ©

theorem MDifferentiableAt.const_smul (hf : MDifferentiableAt I ğ“˜(ğ•œ, E') f z) (s : ğ•œ) :
    MDifferentiableAt I ğ“˜(ğ•œ, E') (s â€¢ f) z :=
  (hf.hasMFDerivAt.const_smul s).mdifferentiableAt

theorem MDifferentiable.const_smul (s : ğ•œ) (hf : MDifferentiable I ğ“˜(ğ•œ, E') f) :
    MDifferentiable I ğ“˜(ğ•œ, E') (s â€¢ f) := fun x => (hf x).const_smul s

theorem const_smul_mfderiv (hf : MDifferentiableAt I ğ“˜(ğ•œ, E') f z) (s : ğ•œ) :
    (mfderiv I ğ“˜(ğ•œ, E') (s â€¢ f) z : TangentSpace I z â†’L[ğ•œ] E') =
      (s â€¢ mfderiv I ğ“˜(ğ•œ, E') f z : TangentSpace I z â†’L[ğ•œ] E') :=
  (hf.hasMFDerivAt.const_smul s).mfderiv

theorem HasMFDerivAt.neg (hf : HasMFDerivAt I ğ“˜(ğ•œ, E') f z f') :
    HasMFDerivAt I ğ“˜(ğ•œ, E') (-f) z (-f') :=
  âŸ¨hf.1.neg, hf.2.negâŸ©

theorem hasMFDerivAt_neg : HasMFDerivAt I ğ“˜(ğ•œ, E') (-f) z (-f') â†” HasMFDerivAt I ğ“˜(ğ•œ, E') f z f' :=
  âŸ¨fun hf => by convert hf.neg <;> rw [neg_neg], fun hf => hf.negâŸ©

theorem MDifferentiableAt.neg (hf : MDifferentiableAt I ğ“˜(ğ•œ, E') f z) :
    MDifferentiableAt I ğ“˜(ğ•œ, E') (-f) z :=
  hf.hasMFDerivAt.neg.mdifferentiableAt

theorem mdifferentiableAt_neg :
    MDifferentiableAt I ğ“˜(ğ•œ, E') (-f) z â†” MDifferentiableAt I ğ“˜(ğ•œ, E') f z :=
  âŸ¨fun hf => by convert hf.neg; rw [neg_neg], fun hf => hf.negâŸ©

theorem MDifferentiable.neg (hf : MDifferentiable I ğ“˜(ğ•œ, E') f) : MDifferentiable I ğ“˜(ğ•œ, E') (-f) :=
  fun x => (hf x).neg

theorem mfderiv_neg (f : M â†’ E') (x : M) :
    (mfderiv I ğ“˜(ğ•œ, E') (-f) x : TangentSpace I x â†’L[ğ•œ] E') =
      (-mfderiv I ğ“˜(ğ•œ, E') f x : TangentSpace I x â†’L[ğ•œ] E') := by
  simp_rw [mfderiv]
  by_cases hf : MDifferentiableAt I ğ“˜(ğ•œ, E') f x
  Â· exact hf.hasMFDerivAt.neg.mfderiv
  Â· rw [if_neg hf]; rw [â† mdifferentiableAt_neg] at hf; rw [if_neg hf, neg_zero]

theorem HasMFDerivAt.sub (hf : HasMFDerivAt I ğ“˜(ğ•œ, E') f z f')
    (hg : HasMFDerivAt I ğ“˜(ğ•œ, E') g z g') : HasMFDerivAt I ğ“˜(ğ•œ, E') (f - g) z (f' - g') :=
  âŸ¨hf.1.sub hg.1, hf.2.sub hg.2âŸ©

theorem MDifferentiableAt.sub (hf : MDifferentiableAt I ğ“˜(ğ•œ, E') f z)
    (hg : MDifferentiableAt I ğ“˜(ğ•œ, E') g z) : MDifferentiableAt I ğ“˜(ğ•œ, E') (f - g) z :=
  (hf.hasMFDerivAt.sub hg.hasMFDerivAt).mdifferentiableAt

theorem MDifferentiable.sub (hf : MDifferentiable I ğ“˜(ğ•œ, E') f)
    (hg : MDifferentiable I ğ“˜(ğ•œ, E') g) : MDifferentiable I ğ“˜(ğ•œ, E') (f - g) := fun x =>
  (hf x).sub (hg x)

theorem mfderiv_sub (hf : MDifferentiableAt I ğ“˜(ğ•œ, E') f z)
    (hg : MDifferentiableAt I ğ“˜(ğ•œ, E') g z) :
    (by exact mfderiv I ğ“˜(ğ•œ, E') (f - g) z : TangentSpace I z â†’L[ğ•œ] E') =
      (by exact mfderiv I ğ“˜(ğ•œ, E') f z) - (by exact mfderiv I ğ“˜(ğ•œ, E') g z) :=
  (hf.hasMFDerivAt.sub hg.hasMFDerivAt).mfderiv

end Group

section AlgebraOverRing

variable {I}
variable {z : M} {F' : Type*} [NormedRing F'] [NormedAlgebra ğ•œ F'] {p q : M â†’ F'}
  {p' q' : TangentSpace I z â†’L[ğ•œ] F'}

theorem HasMFDerivWithinAt.mul' (hp : HasMFDerivWithinAt I ğ“˜(ğ•œ, F') p s z p')
    (hq : HasMFDerivWithinAt I ğ“˜(ğ•œ, F') q s z q') :
    HasMFDerivWithinAt I ğ“˜(ğ•œ, F') (p * q) s z (p z â€¢ q' + p'.smulRight (q z) : E â†’L[ğ•œ] F') :=
  âŸ¨hp.1.mul hq.1, by simpa only [mfld_simps] using hp.2.mul' hq.2âŸ©

theorem HasMFDerivAt.mul' (hp : HasMFDerivAt I ğ“˜(ğ•œ, F') p z p')
    (hq : HasMFDerivAt I ğ“˜(ğ•œ, F') q z q') :
    HasMFDerivAt I ğ“˜(ğ•œ, F') (p * q) z (p z â€¢ q' + p'.smulRight (q z) : E â†’L[ğ•œ] F') :=
  hasMFDerivWithinAt_univ.mp <| hp.hasMFDerivWithinAt.mul' hq.hasMFDerivWithinAt

theorem MDifferentiableWithinAt.mul (hp : MDifferentiableWithinAt I ğ“˜(ğ•œ, F') p s z)
    (hq : MDifferentiableWithinAt I ğ“˜(ğ•œ, F') q s z) :
    MDifferentiableWithinAt I ğ“˜(ğ•œ, F') (p * q) s z :=
  (hp.hasMFDerivWithinAt.mul' hq.hasMFDerivWithinAt).mdifferentiableWithinAt

theorem MDifferentiableAt.mul (hp : MDifferentiableAt I ğ“˜(ğ•œ, F') p z)
    (hq : MDifferentiableAt I ğ“˜(ğ•œ, F') q z) : MDifferentiableAt I ğ“˜(ğ•œ, F') (p * q) z :=
  (hp.hasMFDerivAt.mul' hq.hasMFDerivAt).mdifferentiableAt

theorem MDifferentiableOn.mul (hp : MDifferentiableOn I ğ“˜(ğ•œ, F') p s)
    (hq : MDifferentiableOn I ğ“˜(ğ•œ, F') q s) : MDifferentiableOn I ğ“˜(ğ•œ, F') (p * q) s := fun x hx =>
  (hp x hx).mul <| hq x hx

theorem MDifferentiable.mul (hp : MDifferentiable I ğ“˜(ğ•œ, F') p)
    (hq : MDifferentiable I ğ“˜(ğ•œ, F') q) : MDifferentiable I ğ“˜(ğ•œ, F') (p * q) := fun x =>
  (hp x).mul (hq x)

end AlgebraOverRing

section AlgebraOverCommRing

variable {I}
variable {z : M} {F' : Type*} [NormedCommRing F'] [NormedAlgebra ğ•œ F'] {p q : M â†’ F'}
  {p' q' : TangentSpace I z â†’L[ğ•œ] F'}

theorem HasMFDerivWithinAt.mul (hp : HasMFDerivWithinAt I ğ“˜(ğ•œ, F') p s z p')
    (hq : HasMFDerivWithinAt I ğ“˜(ğ•œ, F') q s z q') :
    HasMFDerivWithinAt I ğ“˜(ğ•œ, F') (p * q) s z (p z â€¢ q' + q z â€¢ p' : E â†’L[ğ•œ] F') := by
  convert hp.mul' hq; ext _; apply mul_comm

theorem HasMFDerivAt.mul (hp : HasMFDerivAt I ğ“˜(ğ•œ, F') p z p')
    (hq : HasMFDerivAt I ğ“˜(ğ•œ, F') q z q') :
    HasMFDerivAt I ğ“˜(ğ•œ, F') (p * q) z (p z â€¢ q' + q z â€¢ p' : E â†’L[ğ•œ] F') :=
  hasMFDerivWithinAt_univ.mp <| hp.hasMFDerivWithinAt.mul hq.hasMFDerivWithinAt

end AlgebraOverCommRing

end Arithmetic

end SpecificFunctions
