/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Floris van Doorn
-/
module

public import Mathlib.Analysis.Calculus.FDeriv.Mul
public import Mathlib.Geometry.Manifold.MFDeriv.FDeriv
import Mathlib.Geometry.Manifold.Notation

/-!
# Differentiability of specific functions

In this file, we establish differentiability results for
- continuous linear maps and continuous linear equivalences
- the identity
- constant functions
- products
- arithmetic operations (such as addition and scalar multiplication).

-/

public section

noncomputable section

open scoped Manifold
open Bundle Set Topology

section SpecificFunctions

/-! ### Differentiability of specific functions -/

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  -- declare a charted space `M` over the pair `(E, H)`.
  {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H] {I : ModelWithCorners ğ•œ E H} {M : Type*}
  [TopologicalSpace M] [ChartedSpace H M]
  -- declare a charted space `M'` over the pair `(E', H')`.
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E'] {H' : Type*} [TopologicalSpace H']
  {I' : ModelWithCorners ğ•œ E' H'} {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M']
  -- declare a charted space `M''` over the pair `(E'', H'')`.
  {E'' : Type*} [NormedAddCommGroup E''] [NormedSpace ğ•œ E'']
  {H'' : Type*} [TopologicalSpace H''] {I'' : ModelWithCorners ğ•œ E'' H''} {M'' : Type*}
  [TopologicalSpace M''] [ChartedSpace H'' M'']
  -- declare a charted space `N` over the pair `(F, G)`.
  {F : Type*}
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] {G : Type*} [TopologicalSpace G]
  {J : ModelWithCorners ğ•œ F G} {N : Type*} [TopologicalSpace N] [ChartedSpace G N]
  -- declare a charted space `N'` over the pair `(F', G')`.
  {F' : Type*}
  [NormedAddCommGroup F'] [NormedSpace ğ•œ F'] {G' : Type*} [TopologicalSpace G']
  {J' : ModelWithCorners ğ•œ F' G'} {N' : Type*} [TopologicalSpace N'] [ChartedSpace G' N']
  -- Fâ‚, Fâ‚‚, Fâ‚ƒ, Fâ‚„ are normed spaces
  {Fâ‚ : Type*}
  [NormedAddCommGroup Fâ‚] [NormedSpace ğ•œ Fâ‚] {Fâ‚‚ : Type*} [NormedAddCommGroup Fâ‚‚]
  [NormedSpace ğ•œ Fâ‚‚] {Fâ‚ƒ : Type*} [NormedAddCommGroup Fâ‚ƒ] [NormedSpace ğ•œ Fâ‚ƒ] {Fâ‚„ : Type*}
  [NormedAddCommGroup Fâ‚„] [NormedSpace ğ•œ Fâ‚„]

namespace ContinuousLinearMap

variable (f : E â†’L[ğ•œ] E') {s : Set E} {x : E}

protected theorem hasMFDerivWithinAt : HasMFDerivAt[s] f x f :=
  f.hasFDerivWithinAt.hasMFDerivWithinAt

protected theorem hasMFDerivAt : HasMFDerivAt% f x f :=
  f.hasFDerivAt.hasMFDerivAt

protected theorem mdifferentiableWithinAt : MDiffAt[s] f x :=
  f.differentiableWithinAt.mdifferentiableWithinAt

protected theorem mdifferentiableOn : MDiff[s] f :=
  f.differentiableOn.mdifferentiableOn

protected theorem mdifferentiableAt : MDiffAt f x :=
  f.differentiableAt.mdifferentiableAt

protected theorem mdifferentiable : MDiff f :=
  f.differentiable.mdifferentiable

theorem mfderiv_eq : mfderiv% f x = f :=
  f.hasMFDerivAt.mfderiv

theorem mfderivWithin_eq (hs : UniqueMDiffWithinAt ğ“˜(ğ•œ, E) s x) : mfderiv[s] f x = f :=
  f.hasMFDerivWithinAt.mfderivWithin hs

end ContinuousLinearMap

namespace ContinuousLinearEquiv

variable (f : E â‰ƒL[ğ•œ] E') {s : Set E} {x : E}

protected theorem hasMFDerivWithinAt : HasMFDerivAt[s] f x (f : E â†’L[ğ•œ] E') :=
  f.hasFDerivWithinAt.hasMFDerivWithinAt

protected theorem hasMFDerivAt : HasMFDerivAt% f x (f : E â†’L[ğ•œ] E') :=
  f.hasFDerivAt.hasMFDerivAt

protected theorem mdifferentiableWithinAt : MDiffAt[s] f x :=
  f.differentiableWithinAt.mdifferentiableWithinAt

protected theorem mdifferentiableOn : MDiff[s] f :=
  f.differentiableOn.mdifferentiableOn

protected theorem mdifferentiableAt : MDiffAt f x :=
  f.differentiableAt.mdifferentiableAt

protected theorem mdifferentiable : MDiff f :=
  f.differentiable.mdifferentiable

theorem mfderiv_eq : mfderiv% f x = (f : E â†’L[ğ•œ] E') :=
  f.hasMFDerivAt.mfderiv

theorem mfderivWithin_eq (hs : UniqueMDiffWithinAt ğ“˜(ğ•œ, E) s x) :
    mfderiv[s] f x = (f : E â†’L[ğ•œ] E') :=
  f.hasMFDerivWithinAt.mfderivWithin hs

end ContinuousLinearEquiv

variable {s : Set M} {x : M}

section id

/-! #### Identity -/

theorem hasMFDerivAt_id (x : M) :
    HasMFDerivAt I I (@id M) x (ContinuousLinearMap.id ğ•œ (TangentSpace I x)) := by
  refine âŸ¨continuousAt_id, ?_âŸ©
  have : âˆ€á¶  y in ğ“[range I] (extChartAt I x) x, (extChartAt I x âˆ˜ (extChartAt I x).symm) y = y := by
    apply Filter.mem_of_superset (extChartAt_target_mem_nhdsWithin x)
    mfld_set_tac
  apply HasFDerivWithinAt.congr_of_eventuallyEq (hasFDerivWithinAt_id _ _) this
  simp only [mfld_simps]

theorem hasMFDerivWithinAt_id (s : Set M) (x : M) :
    HasMFDerivAt[s] (@id M) x (ContinuousLinearMap.id ğ•œ (TangentSpace I x)) :=
  (hasMFDerivAt_id x).hasMFDerivWithinAt

theorem mdifferentiableAt_id : MDiffAt (@id M) x :=
  (hasMFDerivAt_id x).mdifferentiableAt

theorem mdifferentiableWithinAt_id : MDiffAt[s] (@id M) x :=
  mdifferentiableAt_id.mdifferentiableWithinAt

theorem mdifferentiable_id : MDiff (@id M) := fun _ => mdifferentiableAt_id

theorem mdifferentiableOn_id : MDiff[s] (@id M) :=
  mdifferentiable_id.mdifferentiableOn

@[simp, mfld_simps]
theorem mfderiv_id : mfderiv% (@id M) x = ContinuousLinearMap.id ğ•œ (TangentSpace I x) :=
  (hasMFDerivAt_id x).mfderiv

theorem mfderivWithin_id (hxs : UniqueMDiffWithinAt I s x) :
    mfderiv[s] (@id M) x = ContinuousLinearMap.id ğ•œ (TangentSpace I x) := by
  rw [MDifferentiable.mfderivWithin mdifferentiableAt_id hxs]
  exact mfderiv_id

@[simp, mfld_simps]
theorem tangentMap_id : tangentMap I I (id : M â†’ M) = id := by ext1 âŸ¨x, vâŸ©; simp [tangentMap]

theorem tangentMapWithin_id {p : TangentBundle I M} (hs : UniqueMDiffWithinAt I s p.proj) :
    tangentMapWithin I I (id : M â†’ M) s p = p := by
  simp only [tangentMapWithin, id]
  rw [mfderivWithin_id]
  Â· rcases p with âŸ¨âŸ©; rfl
  Â· exact hs

end id

section Const

/-! #### Constants -/


variable {c : M'}

theorem hasMFDerivAt_const (c : M') (x : M) :
    HasMFDerivAt% (fun _ : M => c) x (0 : TangentSpace I x â†’L[ğ•œ] TangentSpace I' c) := by
  refine âŸ¨continuous_const.continuousAt, ?_âŸ©
  simp only [writtenInExtChartAt, Function.comp_def, hasFDerivWithinAt_const]

theorem hasMFDerivWithinAt_const (c : M') (s : Set M) (x : M) :
    HasMFDerivAt[s] (fun _ : M => c) x (0 : TangentSpace I x â†’L[ğ•œ] TangentSpace I' c) :=
  (hasMFDerivAt_const c x).hasMFDerivWithinAt

theorem mdifferentiableAt_const : MDiffAt (fun _ : M => c) x :=
  (hasMFDerivAt_const c x).mdifferentiableAt

theorem mdifferentiableWithinAt_const : MDiffAt[s] (fun _ : M => c) x :=
  mdifferentiableAt_const.mdifferentiableWithinAt

theorem mdifferentiable_const : MDiff fun _ : M => c := fun _ =>
  mdifferentiableAt_const

theorem mdifferentiableOn_const : MDiff[s] (fun _ : M => c) :=
  mdifferentiable_const.mdifferentiableOn

@[simp, mfld_simps]
theorem mfderiv_const :
    mfderiv% (fun _ : M => c) x = (0 : TangentSpace I x â†’L[ğ•œ] TangentSpace I' c) :=
  (hasMFDerivAt_const c x).mfderiv

theorem mfderivWithin_const :
    mfderiv[s] (fun _ : M => c) x = (0 : TangentSpace I x â†’L[ğ•œ] TangentSpace I' c) :=
  (hasMFDerivWithinAt_const _ _ _).mfderivWithin_eq_zero

end Const

section Prod

/-! ### Operations on the product of two manifolds -/

theorem MDifferentiableWithinAt.prodMk {f : M â†’ M'} {g : M â†’ M''}
    (hf : MDiffAt[s] f x) (hg : MDiffAt[s] g x) :
    MDiffAt[s] (fun x => (f x, g x)) x :=
  âŸ¨hf.1.prodMk hg.1, hf.2.prodMk hg.2âŸ©

/-- If `f` and `g` have derivatives `df` and `dg` within `s` at `x`, respectively,
then `x â†¦ (f x, g x)` has derivative `df.prod dg` within `s`. -/
theorem HasMFDerivWithinAt.prodMk {f : M â†’ M'} {g : M â†’ M''}
    {df : TangentSpace I x â†’L[ğ•œ] TangentSpace I' (f x)} (hf : HasMFDerivAt[s] f x df)
    {dg : TangentSpace I x â†’L[ğ•œ] TangentSpace I'' (g x)} (hg : HasMFDerivAt[s] g x dg) :
    HasMFDerivAt[s] (fun y â†¦ (f y, g y)) x (df.prod dg) :=
  âŸ¨hf.1.prodMk hg.1, hf.2.prodMk hg.2âŸ©

lemma mfderivWithin_prodMk {f : M â†’ M'} {g : M â†’ M''} (hf : MDiffAt[s] f x) (hg : MDiffAt[s] g x)
    (hs : UniqueMDiffWithinAt I s x) :
    mfderiv[s] (fun x â†¦ (f x, g x)) x = (mfderiv[s] f x).prod (mfderiv[s] g x) :=
  (hf.hasMFDerivWithinAt.prodMk hg.hasMFDerivWithinAt).mfderivWithin hs

lemma mfderiv_prodMk {f : M â†’ M'} {g : M â†’ M''} (hf : MDiffAt f x) (hg : MDiffAt g x) :
    mfderiv% (fun x â†¦ (f x, g x)) x = (mfderiv% f x).prod (mfderiv% g x) := by
  simp_rw [â† mfderivWithin_univ]
  exact mfderivWithin_prodMk hf.mdifferentiableWithinAt hg.mdifferentiableWithinAt
    (uniqueMDiffWithinAt_univ I)

theorem MDifferentiableAt.prodMk {f : M â†’ M'} {g : M â†’ M''} (hf : MDiffAt f x) (hg : MDiffAt g x) :
    MDiffAt (fun x => (f x, g x)) x :=
  âŸ¨hf.1.prodMk hg.1, hf.2.prodMk hg.2âŸ©

/-- If `f` and `g` have derivatives `df` and `dg` at `x`, respectively,
then `x â†¦ (f x, g x)` has derivative `df.prod dg`. -/
theorem HasMFDerivAt.prodMk {f : M â†’ M'} {g : M â†’ M''}
    {df : TangentSpace I x â†’L[ğ•œ] TangentSpace I' (f x)} (hf : HasMFDerivAt% f x df)
    {dg : TangentSpace I x â†’L[ğ•œ] TangentSpace I'' (g x)} (hg : HasMFDerivAt% g x dg) :
    HasMFDerivAt% (fun y â†¦ (f y, g y)) x (df.prod dg) :=
  âŸ¨hf.1.prodMk hg.1, hf.2.prodMk hg.2âŸ©

theorem MDifferentiableWithinAt.prodMk_space {f : M â†’ E'} {g : M â†’ E''}
    (hf : MDiffAt[s] f x) (hg : MDiffAt[s] g x) :
    MDifferentiableWithinAt I ğ“˜(ğ•œ, E' Ã— E'') (fun x => (f x, g x)) s x :=
  âŸ¨hf.1.prodMk hg.1, hf.2.prodMk hg.2âŸ©

theorem MDifferentiableAt.prodMk_space {f : M â†’ E'} {g : M â†’ E''}
    (hf : MDiffAt f x) (hg : MDiffAt g x) :
    MDifferentiableAt I ğ“˜(ğ•œ, E' Ã— E'') (fun x => (f x, g x)) x :=
  âŸ¨hf.1.prodMk hg.1, hf.2.prodMk hg.2âŸ©

theorem MDifferentiableOn.prodMk {f : M â†’ M'} {g : M â†’ M''} (hf : MDiff[s] f) (hg : MDiff[s] g) :
    MDiff[s] (fun x => (f x, g x)) := fun x hx =>
  (hf x hx).prodMk (hg x hx)

theorem MDifferentiable.prodMk {f : M â†’ M'} {g : M â†’ M''} (hf : MDiff f) (hg : MDiff g) :
    MDiff fun x => (f x, g x) := fun x =>
  (hf x).prodMk (hg x)

theorem MDifferentiableOn.prodMk_space {f : M â†’ E'} {g : M â†’ E''}
    (hf : MDiff[s] f) (hg : MDiff[s] g) :
    MDifferentiableOn I ğ“˜(ğ•œ, E' Ã— E'') (fun x => (f x, g x)) s := fun x hx =>
  (hf x hx).prodMk_space (hg x hx)

theorem MDifferentiable.prodMk_space {f : M â†’ E'} {g : M â†’ E''} (hf : MDiff f) (hg : MDiff g) :
    MDifferentiable I ğ“˜(ğ•œ, E' Ã— E'') fun x => (f x, g x) :=
  fun x => (hf x).prodMk_space (hg x)

theorem hasMFDerivAt_fst (x : M Ã— M') :
    HasMFDerivAt% (@Prod.fst M M') x
      (ContinuousLinearMap.fst ğ•œ (TangentSpace I x.1) (TangentSpace I' x.2)) := by
  refine âŸ¨continuous_fst.continuousAt, ?_âŸ©
  have :
    âˆ€á¶  y in ğ“[range (I.prod I')] extChartAt (I.prod I') x x,
      (extChartAt I x.1 âˆ˜ Prod.fst âˆ˜ (extChartAt (I.prod I') x).symm) y = y.1 := by
    /- porting note: was
    apply Filter.mem_of_superset (extChartAt_target_mem_nhdsWithin (I.prod I') x)
    mfld_set_tac
    -/
    filter_upwards [extChartAt_target_mem_nhdsWithin x] with y hy
    rw [extChartAt_prod] at hy
    exact (extChartAt I x.1).right_inv hy.1
  apply HasFDerivWithinAt.congr_of_eventuallyEq hasFDerivWithinAt_fst this
  -- Porting note: next line was `simp only [mfld_simps]`
  exact (extChartAt I x.1).right_inv <| (extChartAt I x.1).map_source (mem_extChartAt_source _)

theorem hasMFDerivWithinAt_fst (s : Set (M Ã— M')) (x : M Ã— M') :
    HasMFDerivAt[s] (@Prod.fst M M') x
      (ContinuousLinearMap.fst ğ•œ (TangentSpace I x.1) (TangentSpace I' x.2)) :=
  (hasMFDerivAt_fst x).hasMFDerivWithinAt

theorem mdifferentiableAt_fst {x : M Ã— M'} : MDiffAt (@Prod.fst M M') x :=
  (hasMFDerivAt_fst x).mdifferentiableAt

theorem mdifferentiableWithinAt_fst {s : Set (M Ã— M')} {x : M Ã— M'} :
    MDiffAt[s] (@Prod.fst M M') x :=
  mdifferentiableAt_fst.mdifferentiableWithinAt

theorem mdifferentiable_fst : MDiff (@Prod.fst M M') := fun _ => mdifferentiableAt_fst

theorem mdifferentiableOn_fst {s : Set (M Ã— M')} : MDiff[s] (@Prod.fst M M') :=
  mdifferentiable_fst.mdifferentiableOn

@[simp, mfld_simps]
theorem mfderiv_fst {x : M Ã— M'} :
    mfderiv% (@Prod.fst M M') x =
      ContinuousLinearMap.fst ğ•œ (TangentSpace I x.1) (TangentSpace I' x.2) :=
  (hasMFDerivAt_fst x).mfderiv

theorem mfderivWithin_fst {s : Set (M Ã— M')} {x : M Ã— M'}
    (hxs : UniqueMDiffWithinAt (I.prod I') s x) :
    mfderiv[s] (@Prod.fst M M') x =
      ContinuousLinearMap.fst ğ•œ (TangentSpace I x.1) (TangentSpace I' x.2) := by
  rw [MDifferentiable.mfderivWithin mdifferentiableAt_fst hxs]; exact mfderiv_fst

@[simp, mfld_simps]
theorem tangentMap_prodFst {p : TangentBundle (I.prod I') (M Ã— M')} :
    tangentMap (I.prod I') I Prod.fst p = âŸ¨p.proj.1, p.2.1âŸ© := by
  simp [tangentMap]; rfl

theorem tangentMapWithin_prodFst {s : Set (M Ã— M')} {p : TangentBundle (I.prod I') (M Ã— M')}
    (hs : UniqueMDiffWithinAt (I.prod I') s p.proj) :
    tangentMapWithin (I.prod I') I Prod.fst s p = âŸ¨p.proj.1, p.2.1âŸ© := by
  simp only [tangentMapWithin]
  rw [mfderivWithin_fst]
  Â· rcases p with âŸ¨âŸ©; rfl
  Â· exact hs

theorem hasMFDerivAt_snd (x : M Ã— M') :
    HasMFDerivAt% (@Prod.snd M M') x
      (ContinuousLinearMap.snd ğ•œ (TangentSpace I x.1) (TangentSpace I' x.2)) := by
  refine âŸ¨continuous_snd.continuousAt, ?_âŸ©
  have :
    âˆ€á¶  y in ğ“[range (I.prod I')] extChartAt (I.prod I') x x,
      (extChartAt I' x.2 âˆ˜ Prod.snd âˆ˜ (extChartAt (I.prod I') x).symm) y = y.2 := by
    /- porting note: was
    apply Filter.mem_of_superset (extChartAt_target_mem_nhdsWithin (I.prod I') x)
    mfld_set_tac
    -/
    filter_upwards [extChartAt_target_mem_nhdsWithin x] with y hy
    rw [extChartAt_prod] at hy
    exact (extChartAt I' x.2).right_inv hy.2
  apply HasFDerivWithinAt.congr_of_eventuallyEq hasFDerivWithinAt_snd this
  -- Porting note: the next line was `simp only [mfld_simps]`
  exact (extChartAt I' x.2).right_inv <| (extChartAt I' x.2).map_source (mem_extChartAt_source _)

theorem hasMFDerivWithinAt_snd (s : Set (M Ã— M')) (x : M Ã— M') :
    HasMFDerivAt[s] (@Prod.snd M M') x
      (ContinuousLinearMap.snd ğ•œ (TangentSpace I x.1) (TangentSpace I' x.2)) :=
  (hasMFDerivAt_snd x).hasMFDerivWithinAt

theorem mdifferentiableAt_snd {x : M Ã— M'} : MDiffAt (@Prod.snd M M') x :=
  (hasMFDerivAt_snd x).mdifferentiableAt

theorem mdifferentiableWithinAt_snd {s : Set (M Ã— M')} {x : M Ã— M'} :
    MDiffAt[s] (@Prod.snd M M') x := mdifferentiableAt_snd.mdifferentiableWithinAt

theorem mdifferentiable_snd : MDiff (@Prod.snd M M') := fun _ => mdifferentiableAt_snd

theorem mdifferentiableOn_snd {s : Set (M Ã— M')} : MDiff[s] (@Prod.snd M M') :=
  mdifferentiable_snd.mdifferentiableOn

@[simp, mfld_simps]
theorem mfderiv_snd {x : M Ã— M'} :
    mfderiv% (@Prod.snd M M') x =
      ContinuousLinearMap.snd ğ•œ (TangentSpace I x.1) (TangentSpace I' x.2) :=
  (hasMFDerivAt_snd x).mfderiv

theorem mfderivWithin_snd {s : Set (M Ã— M')} {x : M Ã— M'}
    (hxs : UniqueMDiffWithinAt (I.prod I') s x) :
    mfderiv[s] (@Prod.snd M M') x =
      ContinuousLinearMap.snd ğ•œ (TangentSpace I x.1) (TangentSpace I' x.2) := by
  rw [MDifferentiable.mfderivWithin mdifferentiableAt_snd hxs]; exact mfderiv_snd

theorem MDifferentiableWithinAt.fst {f : N â†’ M Ã— M'} {s : Set N} {x : N}
    (hf : MDiffAt[s] f x) : MDiffAt[s] (fun x => (f x).1) x :=
  mdifferentiableAt_fst.comp_mdifferentiableWithinAt x hf

theorem MDifferentiableAt.fst {f : N â†’ M Ã— M'} {x : N} (hf : MDiffAt f x) :
    MDiffAt (fun x => (f x).1) x :=
  mdifferentiableAt_fst.comp x hf

theorem MDifferentiable.fst {f : N â†’ M Ã— M'} (hf : MDiff f) : MDiff fun x => (f x).1 :=
  mdifferentiable_fst.comp hf

theorem MDifferentiableWithinAt.snd {f : N â†’ M Ã— M'} {s : Set N} {x : N} (hf : MDiffAt[s] f x) :
    MDiffAt[s] (fun x => (f x).2) x :=
  mdifferentiableAt_snd.comp_mdifferentiableWithinAt x hf

theorem MDifferentiableAt.snd {f : N â†’ M Ã— M'} {x : N} (hf : MDiffAt f x) :
    MDiffAt (fun x => (f x).2) x :=
  mdifferentiableAt_snd.comp x hf

theorem MDifferentiable.snd {f : N â†’ M Ã— M'} (hf : MDiff f) : MDiff fun x => (f x).2 :=
  mdifferentiable_snd.comp hf

theorem mdifferentiableWithinAt_prod_iff (f : M â†’ M' Ã— N') :
    MDiffAt[s] f x â†” MDiffAt[s] (Prod.fst âˆ˜ f) x âˆ§ MDiffAt[s] (Prod.snd âˆ˜ f) x :=
  âŸ¨fun h => âŸ¨h.fst, h.sndâŸ©, fun h => h.1.prodMk h.2âŸ©

theorem mdifferentiableWithinAt_prod_module_iff (f : M â†’ Fâ‚ Ã— Fâ‚‚) :
    MDifferentiableWithinAt I ğ“˜(ğ•œ, Fâ‚ Ã— Fâ‚‚) f s x â†”
      MDiffAt[s] (Prod.fst âˆ˜ f) x âˆ§ MDiffAt[s] (Prod.snd âˆ˜ f) x := by
  rw [modelWithCornersSelf_prod, â† chartedSpaceSelf_prod]
  exact mdifferentiableWithinAt_prod_iff f

theorem mdifferentiableAt_prod_iff (f : M â†’ M' Ã— N') :
    MDiffAt f x â†” MDiffAt (Prod.fst âˆ˜ f) x âˆ§ MDiffAt (Prod.snd âˆ˜ f) x := by
  simp_rw [â† mdifferentiableWithinAt_univ]; exact mdifferentiableWithinAt_prod_iff f

theorem mdifferentiableAt_prod_module_iff (f : M â†’ Fâ‚ Ã— Fâ‚‚) :
    MDifferentiableAt I ğ“˜(ğ•œ, Fâ‚ Ã— Fâ‚‚) f x â†”
      MDiffAt (Prod.fst âˆ˜ f) x âˆ§ MDiffAt (Prod.snd âˆ˜ f) x := by
  rw [modelWithCornersSelf_prod, â† chartedSpaceSelf_prod]
  exact mdifferentiableAt_prod_iff f

theorem mdifferentiableOn_prod_iff (f : M â†’ M' Ã— N') :
    MDiff[s] f â†” MDiff[s] (Prod.fst âˆ˜ f) âˆ§ MDiff[s] (Prod.snd âˆ˜ f) :=
  âŸ¨fun h â†¦ âŸ¨fun x hx â†¦ ((mdifferentiableWithinAt_prod_iff f).1 (h x hx)).1,
      fun x hx â†¦ ((mdifferentiableWithinAt_prod_iff f).1 (h x hx)).2âŸ©,
    fun h x hx â†¦ (mdifferentiableWithinAt_prod_iff f).2 âŸ¨h.1 x hx, h.2 x hxâŸ©âŸ©

theorem mdifferentiableOn_prod_module_iff (f : M â†’ Fâ‚ Ã— Fâ‚‚) :
    MDifferentiableOn I ğ“˜(ğ•œ, Fâ‚ Ã— Fâ‚‚) f s â†” MDiff[s] (Prod.fst âˆ˜ f) âˆ§ MDiff[s] (Prod.snd âˆ˜ f) := by
  rw [modelWithCornersSelf_prod, â† chartedSpaceSelf_prod]
  exact mdifferentiableOn_prod_iff f

theorem mdifferentiable_prod_iff (f : M â†’ M' Ã— N') :
    MDiff f â†” MDiff (Prod.fst âˆ˜ f) âˆ§ MDiff (Prod.snd âˆ˜ f) :=
  âŸ¨fun h => âŸ¨h.fst, h.sndâŸ©, fun h => by convert h.1.prodMk h.2âŸ©

theorem mdifferentiable_prod_module_iff (f : M â†’ Fâ‚ Ã— Fâ‚‚) :
    MDifferentiable I ğ“˜(ğ•œ, Fâ‚ Ã— Fâ‚‚) f â†” MDiff (Prod.fst âˆ˜ f) âˆ§ MDiff (Prod.snd âˆ˜ f) := by
  rw [modelWithCornersSelf_prod, â† chartedSpaceSelf_prod]
  exact mdifferentiable_prod_iff f


section prodMap

variable {f : M â†’ M'} {g : N â†’ N'} {r : Set N} {y : N}

/-- The product map of two `C^n` functions within a set at a point is `C^n`
within the product set at the product point. -/
theorem MDifferentiableWithinAt.prodMap' {p : M Ã— N}
    (hf : MDiffAt[s] f p.1) (hg : MDiffAt[r] g p.2) :
    MDiffAt[s Ã—Ë¢ r] (Prod.map f g) p :=
  (hf.comp p mdifferentiableWithinAt_fst (prod_subset_preimage_fst _ _)).prodMk <|
    hg.comp p mdifferentiableWithinAt_snd (prod_subset_preimage_snd _ _)

theorem MDifferentiableWithinAt.prodMap (hf : MDiffAt[s] f x) (hg : MDiffAt[r] g y) :
    MDiffAt[s Ã—Ë¢ r] (Prod.map f g) (x, y) :=
  hf.prodMap' hg

theorem MDifferentiableAt.prodMap (hf : MDiffAt f x) (hg : MDiffAt g y) :
    MDiffAt (Prod.map f g) (x, y) := by
  rw [â† mdifferentiableWithinAt_univ] at *
  convert hf.prodMap hg
  exact univ_prod_univ.symm

/-- Variant of `MDifferentiableAt.prod_map` in which the point in the product is given as `p`
instead of a pair `(x, y)`. -/
theorem MDifferentiableAt.prodMap' {p : M Ã— N}
    (hf : MDiffAt f p.1) (hg : MDiffAt g p.2) : MDiffAt (Prod.map f g) p :=
  hf.prodMap hg

theorem MDifferentiableOn.prodMap (hf : MDiff[s] f) (hg : MDiff[r] g) :
    MDiff[s Ã—Ë¢ r] (Prod.map f g) :=
  (hf.comp mdifferentiableOn_fst (prod_subset_preimage_fst _ _)).prodMk <|
    hg.comp mdifferentiableOn_snd (prod_subset_preimage_snd _ _)

theorem MDifferentiable.prodMap (hf : MDiff f) (hg : MDiff g) : MDiff (Prod.map f g) := fun p â†¦
  (hf p.1).prodMap' (hg p.2)

set_option linter.flexible false in -- TODO: fix non-terminal simp_all followed by use
lemma HasMFDerivWithinAt.prodMap {s : Set <| M Ã— M'} {p : M Ã— M'} {f : M â†’ N} {g : M' â†’ N'}
    {df : TangentSpace I p.1 â†’L[ğ•œ] TangentSpace J (f p.1)}
    (hf : HasMFDerivWithinAt I J f (Prod.fst '' s) p.1 df)
    {dg : TangentSpace I' p.2 â†’L[ğ•œ] TangentSpace J' (g p.2)}
    (hg : HasMFDerivWithinAt I' J' g (Prod.snd '' s) p.2 dg) :
    HasMFDerivWithinAt (I.prod I') (J.prod J') (Prod.map f g) s p (df.prodMap dg) := by
  refine âŸ¨hf.1.prodMap hg.1 |>.mono (by grind), ?_âŸ©
  have better : ((extChartAt (I.prod I') p).symm â»Â¹' s âˆ© range â†‘(I.prod I')) âŠ†
      ((extChartAt I p.1).symm â»Â¹' (Prod.fst '' s) âˆ© range I) Ã—Ë¢
        ((extChartAt I' p.2).symm â»Â¹' (Prod.snd '' s) âˆ© range I') := by
    simp only [mfld_simps]
    rw [range_prodMap, I.toPartialEquiv.prod_symm, (chartAt H p.1).toPartialEquiv.prod_symm]
    -- This is very tedious; a nicer proof is welcome!
    intro pâ‚€ âŸ¨hpâ‚€, âŸ¨hpâ‚â‚, hpâ‚â‚‚âŸ©âŸ©
    refine âŸ¨âŸ¨?_, by assumptionâŸ©, âŸ¨?_, by assumptionâŸ©âŸ©
    Â· simp_all
      use (chartAt H' p.2).symm <| I'.symm pâ‚€.2
    Â· simp_all
      use (chartAt H p.1).symm <| I.symm pâ‚€.1
  rw [writtenInExtChart_prod]
  apply HasFDerivWithinAt.mono ?_ better
  apply HasFDerivWithinAt.prodMap
  exacts [hf.2.mono (fst_image_prod_subset ..), hg.2.mono (snd_image_prod_subset ..)]

lemma HasMFDerivAt.prodMap {p : M Ã— M'} {f : M â†’ N} {g : M' â†’ N'}
    {df : TangentSpace I p.1 â†’L[ğ•œ] TangentSpace J (f p.1)} (hf : HasMFDerivAt% f p.1 df)
    {dg : TangentSpace I' p.2 â†’L[ğ•œ] TangentSpace J' (g p.2)} (hg : HasMFDerivAt% g p.2 dg) :
    HasMFDerivAt% (Prod.map f g) p
      ((mfderiv% f p.1).prodMap (mfderiv% g p.2)) := by
  simp_rw [â† hasMFDerivWithinAt_univ, â† mfderivWithin_univ, â† univ_prod_univ]
  convert hf.hasMFDerivWithinAt.prodMap hg.hasMFDerivWithinAt
  Â· rw [mfderivWithin_univ]; exact hf.mfderiv
  Â· rw [mfderivWithin_univ]; exact hg.mfderiv

-- Note: this lemma does not apply easily to an arbitrary subset `s âŠ† M Ã— M'` as
-- unique differentiability on `(Prod.fst '' s)` and `(Prod.snd '' s)` does not imply
-- unique differentiability on `s`: a priori, `(Prod.fst '' s) Ã— (Prod.fst '' s)`
-- could be a strict superset of `s`.
lemma mfderivWithin_prodMap {p : M Ã— M'} {t : Set M'} {f : M â†’ N} {g : M' â†’ N'}
    (hf : MDiffAt[s] f p.1) (hg : MDiffAt[t] g p.2)
    (hs : UniqueMDiffWithinAt I s p.1) (ht : UniqueMDiffWithinAt I' t p.2) :
    mfderiv[s Ã—Ë¢ t] (Prod.map f g) p = (mfderiv[s] f p.1).prodMap (mfderiv[t] g p.2) := by
  have hf' : HasMFDerivAt[Prod.fst '' s Ã—Ë¢ t] f p.1 (mfderiv[s] f p.1) :=
    hf.hasMFDerivWithinAt.mono (by grind)
  have hg' : HasMFDerivAt[Prod.snd '' s Ã—Ë¢ t] g p.2 (mfderiv[t] g p.2) :=
    hg.hasMFDerivWithinAt.mono (by grind)
  exact (hf'.prodMap hg').mfderivWithin (hs.prod ht)

lemma mfderiv_prodMap {p : M Ã— M'} {f : M â†’ N} {g : M' â†’ N'}
    (hf : MDiffAt f p.1) (hg : MDiffAt g p.2) :
    mfderiv% (Prod.map f g) p = (mfderiv% f p.1).prodMap (mfderiv% g p.2) := by
  simp_rw [â† mfderivWithin_univ, â† univ_prod_univ]
  exact mfderivWithin_prodMap hf.mdifferentiableWithinAt hg.mdifferentiableWithinAt
    (uniqueMDiffWithinAt_univ I) (uniqueMDiffWithinAt_univ I')

end prodMap

@[simp, mfld_simps]
theorem tangentMap_prodSnd {p : TangentBundle (I.prod I') (M Ã— M')} :
    tangentMap (I.prod I') I' Prod.snd p = âŸ¨p.proj.2, p.2.2âŸ© := by
  simp [tangentMap]; rfl

theorem tangentMapWithin_prodSnd {s : Set (M Ã— M')} {p : TangentBundle (I.prod I') (M Ã— M')}
    (hs : UniqueMDiffWithinAt (I.prod I') s p.proj) :
    tangentMapWithin (I.prod I') I' Prod.snd s p = âŸ¨p.proj.2, p.2.2âŸ© := by
  simp only [tangentMapWithin]
  rw [mfderivWithin_snd hs]
  rcases p with âŸ¨âŸ©; rfl

-- Kept as an alias for discoverability.
alias MDifferentiableAt.mfderiv_prod := mfderiv_prodMk

theorem mfderiv_prod_left {xâ‚€ : M} {yâ‚€ : M'} :
    mfderiv% (fun (x : M) => (x, yâ‚€)) xâ‚€ =
      ContinuousLinearMap.inl ğ•œ (TangentSpace I xâ‚€) (TangentSpace I' yâ‚€) := by
  refine (mdifferentiableAt_id.mfderiv_prod mdifferentiableAt_const).trans ?_
  rw [mfderiv_id, mfderiv_const, ContinuousLinearMap.inl]

theorem tangentMap_prod_left {p : TangentBundle I M} {yâ‚€ : M'} :
    tangentMap I (I.prod I') (fun x => (x, yâ‚€)) p = âŸ¨(p.1, yâ‚€), (p.2, 0)âŸ© := by
  simp only [tangentMap, mfderiv_prod_left, TotalSpace.mk_inj]
  rfl

theorem mfderiv_prod_right {xâ‚€ : M} {yâ‚€ : M'} :
    mfderiv% (fun (y : M') => (xâ‚€, y)) yâ‚€ =
      ContinuousLinearMap.inr ğ•œ (TangentSpace I xâ‚€) (TangentSpace I' yâ‚€) := by
  refine (mdifferentiableAt_const.mfderiv_prod mdifferentiableAt_id).trans ?_
  rw [mfderiv_id, mfderiv_const, ContinuousLinearMap.inr]

theorem tangentMap_prod_right {p : TangentBundle I' M'} {xâ‚€ : M} :
    tangentMap I' (I.prod I') (fun y => (xâ‚€, y)) p = âŸ¨(xâ‚€, p.1), (0, p.2)âŸ© := by
  simp only [tangentMap, mfderiv_prod_right, TotalSpace.mk_inj]
  rfl

/-- The total derivative of a function in two variables is the sum of the partial derivatives.
  Note that to state this (without casts) we need to be able to see through the definition of
  `TangentSpace`. -/
theorem mfderiv_prod_eq_add {f : M Ã— M' â†’ M''} {p : M Ã— M'}
    (hf : MDiffAt f p) :
    mfderiv% f p =
        mfderiv% (fun z : M Ã— M' => f (z.1, p.2)) p +
        mfderiv% (fun z : M Ã— M' => f (p.1, z.2)) p := by
  erw [mfderiv_comp_of_eq hf (mdifferentiableAt_fst.prodMk mdifferentiableAt_const) rfl,
    mfderiv_comp_of_eq hf (mdifferentiableAt_const.prodMk mdifferentiableAt_snd) rfl,
    â† ContinuousLinearMap.comp_add,
    mdifferentiableAt_fst.mfderiv_prod mdifferentiableAt_const,
    mdifferentiableAt_const.mfderiv_prod mdifferentiableAt_snd, mfderiv_fst,
    mfderiv_snd, mfderiv_const, mfderiv_const]
  symm
  convert ContinuousLinearMap.comp_id <| mfderiv% f (p.1, p.2)
  exact ContinuousLinearMap.coprod_inl_inr

/-- The total derivative of a function in two variables is the sum of the partial derivatives.
  Note that to state this (without casts) we need to be able to see through the definition of
  `TangentSpace`. Version in terms of the one-variable derivatives. -/
theorem mfderiv_prod_eq_add_comp {f : M Ã— M' â†’ M''} {p : M Ã— M'} (hf : MDiffAt f p) :
    mfderiv% f p =
        (mfderiv% (fun z : M => f (z, p.2)) p.1) âˆ˜L (id (ContinuousLinearMap.fst ğ•œ E E') :
          (TangentSpace (I.prod I') p) â†’L[ğ•œ] (TangentSpace I p.1)) +
        (mfderiv% (fun z : M' => f (p.1, z)) p.2) âˆ˜L (id (ContinuousLinearMap.snd ğ•œ E E') :
          (TangentSpace (I.prod I') p) â†’L[ğ•œ] (TangentSpace I' p.2)) := by
  rw [mfderiv_prod_eq_add hf]
  congr
  Â· have : (fun z : M Ã— M' => f (z.1, p.2)) = (fun z : M => f (z, p.2)) âˆ˜ Prod.fst := rfl
    rw [this, mfderiv_comp (I' := I)]
    Â· simp only [mfderiv_fst, id_eq]
      rfl
    Â· exact hf.comp _ (mdifferentiableAt_id.prodMk mdifferentiableAt_const)
    Â· exact mdifferentiableAt_fst
  Â· have : (fun z : M Ã— M' => f (p.1, z.2)) = (fun z : M' => f (p.1, z)) âˆ˜ Prod.snd := rfl
    rw [this, mfderiv_comp (I' := I')]
    Â· simp only [mfderiv_snd, id_eq]
      rfl
    Â· exact hf.comp _ (mdifferentiableAt_const.prodMk mdifferentiableAt_id)
    Â· exact mdifferentiableAt_snd

/-- The total derivative of a function in two variables is the sum of the partial derivatives.
  Note that to state this (without casts) we need to be able to see through the definition of
  `TangentSpace`. Version in terms of the one-variable derivatives. -/
theorem mfderiv_prod_eq_add_apply {f : M Ã— M' â†’ M''} {p : M Ã— M'} {v : TangentSpace (I.prod I') p}
    (hf : MDiffAt f p) :
    mfderiv% f p v =
      mfderiv% (fun z : M => f (z, p.2)) p.1 v.1 + mfderiv% (fun z : M' => f (p.1, z)) p.2 v.2 := by
  rw [mfderiv_prod_eq_add_comp hf]
  rfl

end Prod

section disjointUnion

variable {M' : Type*} [TopologicalSpace M'] [ChartedSpace H M'] {p : M âŠ• M'}

/-- In extended charts at `p`, `Sum.swap` looks like the identity near `p`. -/
lemma writtenInExtChartAt_sumSwap_eventuallyEq_id :
    writtenInExtChartAt I I p Sum.swap =á¶ [ğ“[range I] (I <| chartAt H p p)] id := by
  cases p with
    | inl x =>
      let t := I.symm â»Â¹' (chartAt H x).target âˆ© range I
      have : EqOn (writtenInExtChartAt I I (Sum.inl x) (@Sum.swap M M')) id t := by
        intro y hy
        simp only [writtenInExtChartAt, extChartAt, Sum.swap_inl,
          ChartedSpace.sum_chartAt_inl, ChartedSpace.sum_chartAt_inr]
        dsimp
        rw [Sum.inr_injective.extend_apply, (chartAt H x).right_inv (by grind)]
        exact I.right_inv (by grind)
      apply Filter.eventually_of_mem ?_ this
      rw [Filter.inter_mem_iff]
      refine âŸ¨I.continuousWithinAt_symm.preimage_mem_nhdsWithin ?_, self_mem_nhdsWithinâŸ©
      exact (chartAt H x).open_target.mem_nhds (by simp)
    | inr x =>
      let t := I.symm â»Â¹' (chartAt H x).target âˆ© range I
      have : EqOn (writtenInExtChartAt I I (Sum.inr x) (@Sum.swap M M')) id t := by
        intro y hy
        simp only [writtenInExtChartAt, extChartAt, Sum.swap_inr,
          ChartedSpace.sum_chartAt_inl, ChartedSpace.sum_chartAt_inr]
        dsimp
        rw [Sum.inl_injective.extend_apply, (chartAt H x).right_inv (by grind)]
        exact I.right_inv (by grind)
      apply Filter.eventually_of_mem ?_ this
      rw [Filter.inter_mem_iff]
      refine âŸ¨I.continuousWithinAt_symm.preimage_mem_nhdsWithin ?_, self_mem_nhdsWithinâŸ©
      exact (chartAt H x).open_target.mem_nhds (by simp)

theorem hasMFDerivAt_sumSwap :
    HasMFDerivAt% (@Sum.swap M M') p (ContinuousLinearMap.id ğ•œ (TangentSpace I p)) := by
  refine âŸ¨by fun_prop, ?_âŸ©
  apply (hasFDerivWithinAt_id _ (range I)).congr_of_eventuallyEq
  Â· exact writtenInExtChartAt_sumSwap_eventuallyEq_id
  Â· simp only [mfld_simps]
    cases p <;> simp

@[simp]
theorem mfderivWithin_sumSwap {s : Set (M âŠ• M')} (hs : UniqueMDiffWithinAt I s p) :
    mfderiv[s] (@Sum.swap M M') p = ContinuousLinearMap.id ğ•œ (TangentSpace I p) :=
  hasMFDerivAt_sumSwap.hasMFDerivWithinAt.mfderivWithin hs

@[simp]
theorem mfderiv_sumSwap :
    mfderiv% (@Sum.swap M M') p = ContinuousLinearMap.id ğ•œ (TangentSpace I p) := by
  simpa [mfderivWithin_univ] using (mfderivWithin_sumSwap (uniqueMDiffWithinAt_univ I))

variable {f : M â†’ N} (g : M' â†’ N') {q : M} {q' : M'}

lemma writtenInExtChartAt_sumInl_eventuallyEq_id :
    (writtenInExtChartAt I I q (@Sum.inl M M')) =á¶ [ğ“[Set.range I] (extChartAt I q q)] id := by
  have hmem : I.symm â»Â¹'
      (chartAt H q).target âˆ© Set.range I âˆˆ ğ“[Set.range I] (extChartAt I q q) := by
    rw [â† I.image_eq (chartAt H q).target]
    exact (chartAt H q).extend_image_target_mem_nhds (mem_chart_source H q)
  filter_upwards [hmem] with y hy
  rcases hy with âŸ¨hyT, âŸ¨z, rflâŸ©âŸ©
  simp [writtenInExtChartAt, extChartAt, ChartedSpace.sum_chartAt_inl,
    Sum.inl_injective.extend_apply <| chartAt H q,
    (chartAt H q).right_inv (by simpa [Set.mem_preimage, I.left_inv] using hyT)]

lemma writtenInExtChartAt_sumInr_eventuallyEq_id :
    (writtenInExtChartAt I I q' (@Sum.inr M M')) =á¶ [ğ“[Set.range I] (extChartAt I q' q')] id := by
  have hmem : I.symm â»Â¹'
      (chartAt H q').target âˆ© Set.range I âˆˆ ğ“[Set.range I] (extChartAt I q' q') := by
    rw [â† I.image_eq (chartAt H q').target]
    exact (chartAt H q').extend_image_target_mem_nhds (mem_chart_source H q')
  filter_upwards [hmem] with y hy
  rcases hy with âŸ¨hyT, âŸ¨z, rflâŸ©âŸ©
  simp [writtenInExtChartAt, extChartAt, ChartedSpace.sum_chartAt_inr,
    Sum.inr_injective.extend_apply <| chartAt H q',
    (chartAt H q').right_inv (by simpa [Set.mem_preimage, I.left_inv] using hyT)]

theorem hasMFDerivWithinAt_inl :
    HasMFDerivAt[s] (@Sum.inl M M') q (ContinuousLinearMap.id ğ•œ (TangentSpace I q)) := by
  refine âŸ¨by fun_prop, ?_âŸ©
  have : (writtenInExtChartAt I I q (@Sum.inl M M'))
      =á¶ [ğ“[(extChartAt I q).symm â»Â¹' s âˆ© Set.range I] (extChartAt I q q)] id :=
    writtenInExtChartAt_sumInl_eventuallyEq_id.filter_mono (nhdsWithin_mono _ (fun _y hy â†¦ hy.2))
  exact (hasFDerivWithinAt_id (extChartAt I q q) _).congr_of_eventuallyEq this
    (by simp [writtenInExtChartAt, extChartAt])

theorem hasMFDerivAt_inl :
    HasMFDerivAt% (@Sum.inl M M') q (ContinuousLinearMap.id ğ•œ (TangentSpace I p)) := by
  simpa [HasMFDerivAt, hasMFDerivWithinAt_univ] using hasMFDerivWithinAt_inl (s := Set.univ)

theorem hasMFDerivWithinAt_inr {t : Set M'} :
    HasMFDerivAt[t] (@Sum.inr M M') q' (ContinuousLinearMap.id ğ•œ (TangentSpace I q')) := by
  refine âŸ¨by fun_prop, ?_âŸ©
  have : (writtenInExtChartAt I I q' (@Sum.inr M M'))
      =á¶ [ğ“[(extChartAt I q').symm â»Â¹' t âˆ© Set.range I] (extChartAt I q' q')] id :=
    writtenInExtChartAt_sumInr_eventuallyEq_id.filter_mono (nhdsWithin_mono _ (fun _y hy â†¦ hy.2))
  exact (hasFDerivWithinAt_id (extChartAt I q' q') _).congr_of_eventuallyEq this
    (by simp [writtenInExtChartAt, extChartAt])

theorem hasMFDerivAt_inr :
    HasMFDerivAt% (@Sum.inr M M') q' (ContinuousLinearMap.id ğ•œ (TangentSpace I p)) := by
  simpa [HasMFDerivAt, hasMFDerivWithinAt_univ] using hasMFDerivWithinAt_inr (t := Set.univ)

theorem mfderivWithin_sumInl (hU : UniqueMDiffWithinAt I s q) :
    mfderiv[s] (@Sum.inl M M') q = ContinuousLinearMap.id ğ•œ (TangentSpace I p) :=
  (hasMFDerivWithinAt_inl).mfderivWithin hU

theorem mfderiv_sumInl :
    mfderiv% (@Sum.inl M M') q = ContinuousLinearMap.id ğ•œ (TangentSpace I p) := by
  simpa [mfderivWithin_univ] using (mfderivWithin_sumInl (uniqueMDiffWithinAt_univ I))

theorem mfderivWithin_sumInr {t : Set M'} (hU : UniqueMDiffWithinAt I t q') :
    mfderiv[t] (@Sum.inr M M') q' = ContinuousLinearMap.id ğ•œ (TangentSpace I q') :=
  (hasMFDerivWithinAt_inr).mfderivWithin hU

theorem mfderiv_sumInr :
    mfderiv% (@Sum.inr M M') q' = ContinuousLinearMap.id ğ•œ (TangentSpace I q') := by
  simpa [mfderivWithin_univ] using (mfderivWithin_sumInr (uniqueMDiffWithinAt_univ I))

end disjointUnion

section Arithmetic

/-! #### Arithmetic

Note that in the `HasMFDerivAt` lemmas there is an abuse of the defeq between `E'` and
`TangentSpace ğ“˜(ğ•œ, E') (f z)` (similarly for `g',F',p',q'`). In general this defeq is not
canonical, but in this case (the tangent space of a vector space) it is canonical.
-/

section Group

variable {z : M} {f g : M â†’ E'} {f' g' : TangentSpace I z â†’L[ğ•œ] E'}

theorem HasMFDerivWithinAt.add {s : Set M}
    (hf : HasMFDerivAt[s] f z f') (hg : HasMFDerivAt[s] g z g') :
    HasMFDerivAt[s] (f + g) z (f' + g') :=
  âŸ¨hf.1.add hg.1, hf.2.add hg.2âŸ©

theorem HasMFDerivAt.add (hf : HasMFDerivAt% f z f') (hg : HasMFDerivAt% g z g') :
    HasMFDerivAt% (f + g) z (f' + g') :=
  âŸ¨hf.1.add hg.1, hf.2.add hg.2âŸ©

theorem MDifferentiableWithinAt.add {s : Set M} (hf : MDiffAt[s] f z) (hg : MDiffAt[s] g z) :
    MDiffAt[s] (f + g) z :=
  (hf.hasMFDerivWithinAt.add hg.hasMFDerivWithinAt).mdifferentiableWithinAt

theorem MDifferentiableAt.add (hf : MDiffAt f z) (hg : MDiffAt g z) : MDiffAt (f + g) z :=
  (hf.hasMFDerivAt.add hg.hasMFDerivAt).mdifferentiableAt

theorem MDifferentiableOn.add {s : Set M} (hf : MDiff[s] f) (hg : MDiff[s] g) : MDiff[s] (f + g) :=
  fun x hx â†¦ (hf x hx).add (hg x hx)

theorem MDifferentiable.add (hf : MDiff f) (hg : MDiff g) : MDiff (f + g) :=
  fun x â†¦ (hf x).add (hg x)

-- Porting note: forcing types using `by exact`
theorem mfderiv_add (hf : MDiffAt f z) (hg : MDiffAt g z) :
    (mfderiv% (f + g) z : TangentSpace I z â†’L[ğ•œ] E') =
      (by exact mfderiv% f z) + (by exact mfderiv% g z) :=
  (hf.hasMFDerivAt.add hg.hasMFDerivAt).mfderiv

section sum
variable {Î¹ : Type} {t : Finset Î¹} {f : Î¹ â†’ M â†’ E'} {f' : Î¹ â†’ TangentSpace I z â†’L[ğ•œ] E'}

lemma HasMFDerivWithinAt.sum (hf : âˆ€ i âˆˆ t, HasMFDerivAt[s] (f i) z (f' i)) :
    HasMFDerivAt[s] (âˆ‘ i âˆˆ t, f i) z (âˆ‘ i âˆˆ t, f' i) := by
  classical
  induction t using Finset.induction_on with
  | empty => simpa using hasMFDerivWithinAt_const ..
  | insert i s hi IH => grind [HasMFDerivWithinAt.add]

lemma HasMFDerivAt.sum (hf : âˆ€ i âˆˆ t, HasMFDerivAt% (f i) z (f' i)) :
    HasMFDerivAt% (âˆ‘ i âˆˆ t, f i) z (âˆ‘ i âˆˆ t, f' i) := by
  simp_all only [â† hasMFDerivWithinAt_univ]
  exact HasMFDerivWithinAt.sum hf

lemma MDifferentiableWithinAt.sum
    (hf : âˆ€ i âˆˆ t, MDiffAt[s] (f i) z) : MDiffAt[s] (âˆ‘ i âˆˆ t, f i) z :=
  (HasMFDerivWithinAt.sum fun i hi â†¦ (hf i hi).hasMFDerivWithinAt).mdifferentiableWithinAt

lemma MDifferentiableAt.sum (hf : âˆ€ i âˆˆ t, MDiffAt (f i) z) : MDiffAt (âˆ‘ i âˆˆ t, f i) z := by
  simp_all only [â† mdifferentiableWithinAt_univ]
  exact .sum hf

lemma MDifferentiableOn.sum (hf : âˆ€ i âˆˆ t, MDiff[s] (f i)) : MDiff[s] (âˆ‘ i âˆˆ t, f i) :=
  fun z hz â†¦ .sum fun i hi â†¦ hf i hi z hz

lemma MDifferentiable.sum (hf : âˆ€ i âˆˆ t, MDiff (f i)) : MDiff (âˆ‘ i âˆˆ t, f i) :=
  fun z â†¦ .sum fun i hi â†¦ hf i hi z

end sum

theorem HasMFDerivAt.const_smul (hf : HasMFDerivAt% f z f') (s : ğ•œ) :
    HasMFDerivAt% (s â€¢ f) z (s â€¢ f') :=
  âŸ¨hf.1.const_smul s, hf.2.const_smul sâŸ©

theorem MDifferentiableAt.const_smul (hf : MDiffAt f z) (s : ğ•œ) : MDiffAt (s â€¢ f) z :=
  (hf.hasMFDerivAt.const_smul s).mdifferentiableAt

theorem MDifferentiable.const_smul (s : ğ•œ) (hf : MDiff f) : MDiff (s â€¢ f) :=
  fun x â†¦ (hf x).const_smul s

theorem const_smul_mfderiv (hf : MDiffAt f z) (s : ğ•œ) : mfderiv% (s â€¢ f) z = s â€¢ mfderiv% f z :=
  (hf.hasMFDerivAt.const_smul s).mfderiv

theorem HasMFDerivWithinAt.neg {s : Set M} (hf : HasMFDerivAt[s] f z f') :
    HasMFDerivAt[s] (-f) z (-f') :=
  âŸ¨hf.1.neg, hf.2.negâŸ©

theorem HasMFDerivAt.neg (hf : HasMFDerivAt% f z f') : HasMFDerivAt% (-f) z (-f') :=
  âŸ¨hf.1.neg, hf.2.negâŸ©

theorem hasMFDerivAt_neg : HasMFDerivAt% (-f) z (-f') â†” HasMFDerivAt% f z f' :=
  âŸ¨fun hf => by convert hf.neg <;> rw [neg_neg], fun hf => hf.negâŸ©

theorem MDifferentiableWithinAt.neg {s : Set M} (hf : MDiffAt[s] f z) : MDiffAt[s] (-f) z :=
  (hf.hasMFDerivWithinAt.neg).mdifferentiableWithinAt

theorem MDifferentiableAt.neg (hf : MDiffAt f z) : MDiffAt (-f) z :=
  hf.hasMFDerivAt.neg.mdifferentiableAt

theorem MDifferentiableOn.neg {s : Set M} (hf : MDiff[s] f) : MDiff[s] (-f) :=
  fun x hx â†¦ (hf x hx).neg

theorem mdifferentiableAt_neg : MDiffAt (-f) z â†” MDiffAt f z :=
  âŸ¨fun hf => by convert hf.neg; rw [neg_neg], fun hf => hf.negâŸ©

theorem MDifferentiable.neg (hf : MDiff f) : MDiff (-f) := fun x â†¦ (hf x).neg

theorem mfderiv_neg (f : M â†’ E') (x : M) : mfderiv% (-f) x = -mfderiv% f x := by
  simp_rw [mfderiv]
  by_cases hf : MDiffAt f x
  Â· exact hf.hasMFDerivAt.neg.mfderiv
  Â· rw [if_neg hf]; rw [â† mdifferentiableAt_neg] at hf; rw [if_neg hf, neg_zero]

theorem HasMFDerivAt.sub (hf : HasMFDerivAt% f z f') (hg : HasMFDerivAt% g z g') :
    HasMFDerivAt% (f - g) z (f' - g') :=
  âŸ¨hf.1.sub hg.1, hf.2.sub hg.2âŸ©

theorem MDifferentiableAt.sub (hf : MDiffAt f z) (hg : MDiffAt g z) : MDiffAt (f - g) z :=
  (hf.hasMFDerivAt.sub hg.hasMFDerivAt).mdifferentiableAt

theorem MDifferentiable.sub (hf : MDiff f) (hg : MDiff g) : MDiff (f - g) :=
  fun x â†¦ (hf x).sub (hg x)

theorem mfderiv_sub (hf : MDiffAt f z) (hg : MDiffAt g z) :
    (by exact mfderiv% (f - g) z : TangentSpace I z â†’L[ğ•œ] E') =
      (by exact mfderiv% f z) - (by exact mfderiv% g z) :=
  (hf.hasMFDerivAt.sub hg.hasMFDerivAt).mfderiv

end Group

section AlgebraOverRing
open scoped RightActions

variable {z : M} {F' : Type*} [NormedRing F'] [NormedAlgebra ğ•œ F'] {p q : M â†’ F'}
  {p' q' : TangentSpace I z â†’L[ğ•œ] F'}

theorem HasMFDerivWithinAt.mul' (hp : HasMFDerivWithinAt I ğ“˜(ğ•œ, F') p s z p')
    (hq : HasMFDerivWithinAt I ğ“˜(ğ•œ, F') q s z q') :
    HasMFDerivWithinAt I ğ“˜(ğ•œ, F') (p * q) s z (p z â€¢ q' + p' <â€¢ q z : E â†’L[ğ•œ] F') :=
  âŸ¨hp.1.mul hq.1, by simpa only [mfld_simps] using hp.2.mul' hq.2âŸ©

theorem HasMFDerivAt.mul' (hp : HasMFDerivAt I ğ“˜(ğ•œ, F') p z p')
    (hq : HasMFDerivAt I ğ“˜(ğ•œ, F') q z q') :
    HasMFDerivAt I ğ“˜(ğ•œ, F') (p * q) z (p z â€¢ q' + p' <â€¢ q z : E â†’L[ğ•œ] F') :=
  hasMFDerivWithinAt_univ.mp <| hp.hasMFDerivWithinAt.mul' hq.hasMFDerivWithinAt

theorem MDifferentiableWithinAt.mul (hp : MDifferentiableWithinAt I ğ“˜(ğ•œ, F') p s z)
    (hq : MDifferentiableWithinAt I ğ“˜(ğ•œ, F') q s z) :
    MDifferentiableWithinAt I ğ“˜(ğ•œ, F') (p * q) s z :=
  (hp.hasMFDerivWithinAt.mul' hq.hasMFDerivWithinAt).mdifferentiableWithinAt

theorem MDifferentiableAt.mul (hp : MDifferentiableAt I ğ“˜(ğ•œ, F') p z)
    (hq : MDifferentiableAt I ğ“˜(ğ•œ, F') q z) : MDifferentiableAt I ğ“˜(ğ•œ, F') (p * q) z :=
  (hp.hasMFDerivAt.mul' hq.hasMFDerivAt).mdifferentiableAt

theorem MDifferentiableOn.mul (hp : MDifferentiableOn I ğ“˜(ğ•œ, F') p s)
    (hq : MDifferentiableOn I ğ“˜(ğ•œ, F') q s) : MDifferentiableOn I ğ“˜(ğ•œ, F') (p * q) s := fun x hx =>
  (hp x hx).mul <| hq x hx

theorem MDifferentiable.mul (hp : MDifferentiable I ğ“˜(ğ•œ, F') p)
    (hq : MDifferentiable I ğ“˜(ğ•œ, F') q) : MDifferentiable I ğ“˜(ğ•œ, F') (p * q) := fun x =>
  (hp x).mul (hq x)

theorem MDifferentiableWithinAt.pow (hp : MDifferentiableWithinAt I ğ“˜(ğ•œ, F') p s z)
    (n : â„•) : MDifferentiableWithinAt I ğ“˜(ğ•œ, F') (p ^ n) s z := by
  induction n with
  | zero => simpa [pow_zero] using mdifferentiableWithinAt_const
  | succ n hn => simpa [pow_succ] using hn.mul hp

theorem MDifferentiableAt.pow (hp : MDifferentiableAt I ğ“˜(ğ•œ, F') p z) (n : â„•) :
    MDifferentiableAt I ğ“˜(ğ•œ, F') (p ^ n) z :=
  mdifferentiableWithinAt_univ.mp (hp.mdifferentiableWithinAt.pow n)

theorem MDifferentiableOn.pow (hp : MDifferentiableOn I ğ“˜(ğ•œ, F') p s) (n : â„•) :
    MDifferentiableOn I ğ“˜(ğ•œ, F') (p ^ n) s := fun x hx => (hp x hx).pow n

theorem MDifferentiable.pow (hp : MDifferentiable I ğ“˜(ğ•œ, F') p) (n : â„•) :
    MDifferentiable I ğ“˜(ğ•œ, F') (p ^ n) := fun x => (hp x).pow n

end AlgebraOverRing

section AlgebraOverCommRing

variable {z : M} {F' : Type*} [NormedCommRing F'] [NormedAlgebra ğ•œ F'] {p q : M â†’ F'}
  {p' q' : TangentSpace I z â†’L[ğ•œ] F'}

theorem HasMFDerivWithinAt.mul (hp : HasMFDerivWithinAt I ğ“˜(ğ•œ, F') p s z p')
    (hq : HasMFDerivWithinAt I ğ“˜(ğ•œ, F') q s z q') :
    HasMFDerivWithinAt I ğ“˜(ğ•œ, F') (p * q) s z (p z â€¢ q' + q z â€¢ p' : E â†’L[ğ•œ] F') := by
  convert hp.mul' hq; ext _; apply mul_comm

theorem HasMFDerivAt.mul (hp : HasMFDerivAt I ğ“˜(ğ•œ, F') p z p')
    (hq : HasMFDerivAt I ğ“˜(ğ•œ, F') q z q') :
    HasMFDerivAt I ğ“˜(ğ•œ, F') (p * q) z (p z â€¢ q' + q z â€¢ p' : E â†’L[ğ•œ] F') :=
  hasMFDerivWithinAt_univ.mp <| hp.hasMFDerivWithinAt.mul hq.hasMFDerivWithinAt

section prod
variable {Î¹ : Type} {t : Finset Î¹} {f : Î¹ â†’ M â†’ F'} {f' : Î¹ â†’ TangentSpace I z â†’L[ğ•œ] F'}

lemma HasMFDerivWithinAt.prod [DecidableEq Î¹]
    (hf : âˆ€ i âˆˆ t, HasMFDerivWithinAt I ğ“˜(ğ•œ, F') (f i) s z (f' i)) :
    HasMFDerivWithinAt I ğ“˜(ğ•œ, F') (âˆ i âˆˆ t, f i) s z
      (âˆ‘ i âˆˆ t, (âˆ j âˆˆ t.erase i, f j z) â€¢ (f' i)) := by
  classical
  induction t using Finset.induction_on with
  | empty => simpa using hasMFDerivWithinAt_const ..
  | insert i t hi IH =>
    rw [t.sum_insert hi, t.erase_insert hi, t.prod_insert hi, add_comm]
    rw [t.forall_mem_insert] at hf
    convert hf.1.mul (IH hf.2) using 2
    Â· simp only [t.smul_sum, â† mul_smul]
      refine t.sum_congr rfl (fun j hj â†¦ ?_)
      rw [t.erase_insert_of_ne (by grind), Finset.prod_insert (by grind)]
    Â· simp

lemma HasMFDerivAt.prod [DecidableEq Î¹]
    (hf : âˆ€ i âˆˆ t, HasMFDerivAt I ğ“˜(ğ•œ, F') (f i) z (f' i)) :
    HasMFDerivAt I ğ“˜(ğ•œ, F') (âˆ i âˆˆ t, f i) z (âˆ‘ i âˆˆ t, (âˆ j âˆˆ t.erase i, f j z) â€¢ (f' i)) := by
  simp_all only [â† hasMFDerivWithinAt_univ]
  exact HasMFDerivWithinAt.prod hf

lemma MDifferentiableWithinAt.prod
    (hf : âˆ€ i âˆˆ t, MDifferentiableWithinAt I ğ“˜(ğ•œ, F') (f i) s z) :
    MDifferentiableWithinAt I ğ“˜(ğ•œ, F') (âˆ i âˆˆ t, f i) s z := by
  -- `by classical exact` to avoid needing a `DecidableEq` argument
  classical exact (HasMFDerivWithinAt.prod
    fun i hi â†¦ (hf i hi).hasMFDerivWithinAt).mdifferentiableWithinAt

lemma MDifferentiableAt.prod (hf : âˆ€ i âˆˆ t, MDifferentiableAt I ğ“˜(ğ•œ, F') (f i) z) :
    MDifferentiableAt I ğ“˜(ğ•œ, F') (âˆ i âˆˆ t, f i) z := by
  simp_all only [â† mdifferentiableWithinAt_univ]
  exact MDifferentiableWithinAt.prod hf

lemma MDifferentiableOn.prod (hf : âˆ€ i âˆˆ t, MDifferentiableOn I ğ“˜(ğ•œ, F') (f i) s) :
    MDifferentiableOn I ğ“˜(ğ•œ, F') (âˆ i âˆˆ t, f i) s :=
  fun z hz â†¦ .prod fun i hi â†¦ hf i hi z hz

lemma MDifferentiable.prod (hf : âˆ€ i âˆˆ t, MDifferentiable I ğ“˜(ğ•œ, F') (f i)) :
    MDifferentiable I ğ“˜(ğ•œ, F') (âˆ i âˆˆ t, f i) :=
  fun z â†¦ .prod fun i hi â†¦ hf i hi z

end prod

end AlgebraOverCommRing

end Arithmetic

end SpecificFunctions
