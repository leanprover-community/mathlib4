/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Floris van Doorn
-/
import Mathlib.Geometry.Manifold.MFDeriv.SpecificFunctions

/-!
# Differentiability of models with corners and (extended) charts

In this file, we analyse the differentiability of charts, models with corners and extended charts.
We show that
* models with corners are differentiable
* charts are differentiable on their source
* `mdifferentiableOn_extChartAt`: `extChartAt` is differentiable on its source

Suppose a partial homeomorphism `e` is differentiable. This file shows
* `PartialHomeomorph.MDifferentiable.mfderiv`: its derivative is a continuous linear equivalence
* `PartialHomeomorph.MDifferentiable.mfderiv_bijective`: its derivative is bijective;
  there are also spelling with trivial kernel and full range

In particular, (extended) charts have bijective differential.

## Tags
charts, differentiable, bijective
-/

noncomputable section

open scoped Manifold
open Bundle Set Topology

variable {ð•œ : Type*} [NontriviallyNormedField ð•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ð•œ E] {H : Type*} [TopologicalSpace H]
  (I : ModelWithCorners ð•œ E H) {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ð•œ E'] {H' : Type*} [TopologicalSpace H']
  (I' : ModelWithCorners ð•œ E' H') {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M']
  {E'' : Type*} [NormedAddCommGroup E''] [NormedSpace ð•œ E''] {H'' : Type*} [TopologicalSpace H'']
  (I'' : ModelWithCorners ð•œ E'' H'') {M'' : Type*} [TopologicalSpace M''] [ChartedSpace H'' M'']

section ModelWithCorners
namespace ModelWithCorners

/-! #### Model with corners -/

protected theorem hasMFDerivAt {x} : HasMFDerivAt I ð“˜(ð•œ, E) I x (ContinuousLinearMap.id _ _) :=
  âŸ¨I.continuousAt, (hasFDerivWithinAt_id _ _).congr' I.rightInvOn (mem_range_self _)âŸ©
#align model_with_corners.has_mfderiv_at ModelWithCorners.hasMFDerivAt

protected theorem hasMFDerivWithinAt {s x} :
    HasMFDerivWithinAt I ð“˜(ð•œ, E) I s x (ContinuousLinearMap.id _ _) :=
  I.hasMFDerivAt.hasMFDerivWithinAt
#align model_with_corners.has_mfderiv_within_at ModelWithCorners.hasMFDerivWithinAt

protected theorem mdifferentiableWithinAt {s x} : MDifferentiableWithinAt I ð“˜(ð•œ, E) I s x :=
  I.hasMFDerivWithinAt.mdifferentiableWithinAt
#align model_with_corners.mdifferentiable_within_at ModelWithCorners.mdifferentiableWithinAt

protected theorem mdifferentiableAt {x} : MDifferentiableAt I ð“˜(ð•œ, E) I x :=
  I.hasMFDerivAt.mdifferentiableAt
#align model_with_corners.mdifferentiable_at ModelWithCorners.mdifferentiableAt

protected theorem mdifferentiableOn {s} : MDifferentiableOn I ð“˜(ð•œ, E) I s := fun _ _ =>
  I.mdifferentiableWithinAt
#align model_with_corners.mdifferentiable_on ModelWithCorners.mdifferentiableOn

protected theorem mdifferentiable : MDifferentiable I ð“˜(ð•œ, E) I := fun _ => I.mdifferentiableAt
#align model_with_corners.mdifferentiable ModelWithCorners.mdifferentiable

theorem hasMFDerivWithinAt_symm {x} (hx : x âˆˆ range I) :
    HasMFDerivWithinAt ð“˜(ð•œ, E) I I.symm (range I) x (ContinuousLinearMap.id _ _) :=
  âŸ¨I.continuousWithinAt_symm,
    (hasFDerivWithinAt_id _ _).congr' (fun _y hy => I.rightInvOn hy.1) âŸ¨hx, mem_range_self _âŸ©âŸ©
#align model_with_corners.has_mfderiv_within_at_symm ModelWithCorners.hasMFDerivWithinAt_symm

theorem mdifferentiableOn_symm : MDifferentiableOn ð“˜(ð•œ, E) I I.symm (range I) := fun _x hx =>
  (I.hasMFDerivWithinAt_symm hx).mdifferentiableWithinAt
#align model_with_corners.mdifferentiable_on_symm ModelWithCorners.mdifferentiableOn_symm

end ModelWithCorners

end ModelWithCorners

section Charts

variable [SmoothManifoldWithCorners I M] [SmoothManifoldWithCorners I' M']
  [SmoothManifoldWithCorners I'' M''] {e : PartialHomeomorph M H}

theorem mdifferentiableAt_atlas (h : e âˆˆ atlas H M) {x : M} (hx : x âˆˆ e.source) :
    MDifferentiableAt I I e x := by
  rw [mdifferentiableAt_iff]
  refine âŸ¨(e.continuousOn x hx).continuousAt (e.open_source.mem_nhds hx), ?_âŸ©
  have mem :
    I ((chartAt H x : M â†’ H) x) âˆˆ I.symm â»Â¹' ((chartAt H x).symm â‰«â‚• e).source âˆ© range I := by
    simp only [hx, mfld_simps]
  have : (chartAt H x).symm.trans e âˆˆ contDiffGroupoid âˆž I :=
    HasGroupoid.compatible (chart_mem_atlas H x) h
  have A :
    ContDiffOn ð•œ âˆž (I âˆ˜ (chartAt H x).symm.trans e âˆ˜ I.symm)
      (I.symm â»Â¹' ((chartAt H x).symm.trans e).source âˆ© range I) :=
    this.1
  have B := A.differentiableOn le_top (I ((chartAt H x : M â†’ H) x)) mem
  simp only [mfld_simps] at B
  rw [inter_comm, differentiableWithinAt_inter] at B
  Â· simpa only [mfld_simps]
  Â· apply IsOpen.mem_nhds ((PartialHomeomorph.open_source _).preimage I.continuous_symm) mem.1
#align mdifferentiable_at_atlas mdifferentiableAt_atlas

theorem mdifferentiableOn_atlas (h : e âˆˆ atlas H M) : MDifferentiableOn I I e e.source :=
  fun _x hx => (mdifferentiableAt_atlas I h hx).mdifferentiableWithinAt
#align mdifferentiable_on_atlas mdifferentiableOn_atlas

theorem mdifferentiableAt_atlas_symm (h : e âˆˆ atlas H M) {x : H} (hx : x âˆˆ e.target) :
    MDifferentiableAt I I e.symm x := by
  rw [mdifferentiableAt_iff]
  refine âŸ¨(e.continuousOn_symm x hx).continuousAt (e.open_target.mem_nhds hx), ?_âŸ©
  have mem : I x âˆˆ I.symm â»Â¹' (e.symm â‰«â‚• chartAt H (e.symm x)).source âˆ© range I := by
    simp only [hx, mfld_simps]
  have : e.symm.trans (chartAt H (e.symm x)) âˆˆ contDiffGroupoid âˆž I :=
    HasGroupoid.compatible h (chart_mem_atlas H _)
  have A :
    ContDiffOn ð•œ âˆž (I âˆ˜ e.symm.trans (chartAt H (e.symm x)) âˆ˜ I.symm)
      (I.symm â»Â¹' (e.symm.trans (chartAt H (e.symm x))).source âˆ© range I) :=
    this.1
  have B := A.differentiableOn le_top (I x) mem
  simp only [mfld_simps] at B
  rw [inter_comm, differentiableWithinAt_inter] at B
  Â· simpa only [mfld_simps]
  Â· apply IsOpen.mem_nhds ((PartialHomeomorph.open_source _).preimage I.continuous_symm) mem.1
#align mdifferentiable_at_atlas_symm mdifferentiableAt_atlas_symm

theorem mdifferentiableOn_atlas_symm (h : e âˆˆ atlas H M) : MDifferentiableOn I I e.symm e.target :=
  fun _x hx => (mdifferentiableAt_atlas_symm I h hx).mdifferentiableWithinAt
#align mdifferentiable_on_atlas_symm mdifferentiableOn_atlas_symm

theorem mdifferentiable_of_mem_atlas (h : e âˆˆ atlas H M) : e.MDifferentiable I I :=
  âŸ¨mdifferentiableOn_atlas I h, mdifferentiableOn_atlas_symm I hâŸ©
#align mdifferentiable_of_mem_atlas mdifferentiable_of_mem_atlas

theorem mdifferentiable_chart (x : M) : (chartAt H x).MDifferentiable I I :=
  mdifferentiable_of_mem_atlas _ (chart_mem_atlas _ _)
#align mdifferentiable_chart mdifferentiable_chart

/-- The derivative of the chart at a base point is the chart of the tangent bundle, composed with
the identification between the tangent bundle of the model space and the product space. -/
theorem tangentMap_chart {p q : TangentBundle I M} (h : q.1 âˆˆ (chartAt H p.1).source) :
    tangentMap I I (chartAt H p.1) q =
      (TotalSpace.toProd _ _).symm
        ((chartAt (ModelProd H E) p : TangentBundle I M â†’ ModelProd H E) q) := by
  dsimp [tangentMap]
  rw [MDifferentiableAt.mfderiv]
  Â· rfl
  Â· exact mdifferentiableAt_atlas _ (chart_mem_atlas _ _) h
#align tangent_map_chart tangentMap_chart

/-- The derivative of the inverse of the chart at a base point is the inverse of the chart of the
tangent bundle, composed with the identification between the tangent bundle of the model space and
the product space. -/
theorem tangentMap_chart_symm {p : TangentBundle I M} {q : TangentBundle I H}
    (h : q.1 âˆˆ (chartAt H p.1).target) :
    tangentMap I I (chartAt H p.1).symm q =
      (chartAt (ModelProd H E) p).symm (TotalSpace.toProd H E q) := by
  dsimp only [tangentMap]
  rw [MDifferentiableAt.mfderiv (mdifferentiableAt_atlas_symm _ (chart_mem_atlas _ _) h)]
  simp only [ContinuousLinearMap.coe_coe, TangentBundle.chartAt, h, tangentBundleCore,
    mfld_simps, (Â· âˆ˜ Â·)]
  -- `simp` fails to apply `PartialEquiv.prod_symm` with `ModelProd`
  congr
  exact ((chartAt H (TotalSpace.proj p)).right_inv h).symm
#align tangent_map_chart_symm tangentMap_chart_symm

lemma mfderiv_chartAt_eq_tangentCoordChange {x y : M} (hsrc : x âˆˆ (chartAt H y).source) :
    mfderiv I I (chartAt H y) x = tangentCoordChange I x y x := by
  have := mdifferentiableAt_atlas I (ChartedSpace.chart_mem_atlas _) hsrc
  simp [mfderiv, if_pos this, Function.comp.assoc]

end Charts


/-! ### Differentiable partial homeomorphisms -/

namespace PartialHomeomorph.MDifferentiable
variable {I I' I''}
variable {e : PartialHomeomorph M M'} (he : e.MDifferentiable I I') {e' : PartialHomeomorph M' M''}

nonrec theorem symm : e.symm.MDifferentiable I' I := he.symm
#align local_homeomorph.mdifferentiable.symm PartialHomeomorph.MDifferentiable.symm

protected theorem mdifferentiableAt {x : M} (hx : x âˆˆ e.source) : MDifferentiableAt I I' e x :=
  (he.1 x hx).mdifferentiableAt (e.open_source.mem_nhds hx)
#align local_homeomorph.mdifferentiable.mdifferentiable_at PartialHomeomorph.MDifferentiable.mdifferentiableAt

theorem mdifferentiableAt_symm {x : M'} (hx : x âˆˆ e.target) : MDifferentiableAt I' I e.symm x :=
  (he.2 x hx).mdifferentiableAt (e.open_target.mem_nhds hx)
#align local_homeomorph.mdifferentiable.mdifferentiable_at_symm PartialHomeomorph.MDifferentiable.mdifferentiableAt_symm

variable [SmoothManifoldWithCorners I M] [SmoothManifoldWithCorners I' M']
  [SmoothManifoldWithCorners I'' M'']

theorem symm_comp_deriv {x : M} (hx : x âˆˆ e.source) :
    (mfderiv I' I e.symm (e x)).comp (mfderiv I I' e x) =
      ContinuousLinearMap.id ð•œ (TangentSpace I x) := by
  have : mfderiv I I (e.symm âˆ˜ e) x = (mfderiv I' I e.symm (e x)).comp (mfderiv I I' e x) :=
    mfderiv_comp x (he.mdifferentiableAt_symm (e.map_source hx)) (he.mdifferentiableAt hx)
  rw [â† this]
  have : mfderiv I I (_root_.id : M â†’ M) x = ContinuousLinearMap.id _ _ := mfderiv_id I
  rw [â† this]
  apply Filter.EventuallyEq.mfderiv_eq
  have : e.source âˆˆ ð“ x := e.open_source.mem_nhds hx
  exact Filter.mem_of_superset this (by mfld_set_tac)
#align local_homeomorph.mdifferentiable.symm_comp_deriv PartialHomeomorph.MDifferentiable.symm_comp_deriv

theorem comp_symm_deriv {x : M'} (hx : x âˆˆ e.target) :
    (mfderiv I I' e (e.symm x)).comp (mfderiv I' I e.symm x) =
      ContinuousLinearMap.id ð•œ (TangentSpace I' x) :=
  he.symm.symm_comp_deriv hx
#align local_homeomorph.mdifferentiable.comp_symm_deriv PartialHomeomorph.MDifferentiable.comp_symm_deriv

/-- The derivative of a differentiable partial homeomorphism, as a continuous linear equivalence
between the tangent spaces at `x` and `e x`. -/
protected def mfderiv {x : M} (hx : x âˆˆ e.source) : TangentSpace I x â‰ƒL[ð•œ] TangentSpace I' (e x) :=
  { mfderiv I I' e x with
    invFun := mfderiv I' I e.symm (e x)
    continuous_toFun := (mfderiv I I' e x).cont
    continuous_invFun := (mfderiv I' I e.symm (e x)).cont
    left_inv := fun y => by
      have : (ContinuousLinearMap.id _ _ : TangentSpace I x â†’L[ð•œ] TangentSpace I x) y = y := rfl
      conv_rhs => rw [â† this, â† he.symm_comp_deriv hx]
      rfl
    right_inv := fun y => by
      have :
        (ContinuousLinearMap.id ð•œ _ : TangentSpace I' (e x) â†’L[ð•œ] TangentSpace I' (e x)) y = y :=
        rfl
      conv_rhs => rw [â† this, â† he.comp_symm_deriv (e.map_source hx)]
      rw [e.left_inv hx]
      rfl }
#align local_homeomorph.mdifferentiable.mfderiv PartialHomeomorph.MDifferentiable.mfderiv

theorem mfderiv_bijective {x : M} (hx : x âˆˆ e.source) : Function.Bijective (mfderiv I I' e x) :=
  (he.mfderiv hx).bijective
#align local_homeomorph.mdifferentiable.mfderiv_bijective PartialHomeomorph.MDifferentiable.mfderiv_bijective

theorem mfderiv_injective {x : M} (hx : x âˆˆ e.source) : Function.Injective (mfderiv I I' e x) :=
  (he.mfderiv hx).injective
#align local_homeomorph.mdifferentiable.mfderiv_injective PartialHomeomorph.MDifferentiable.mfderiv_injective

theorem mfderiv_surjective {x : M} (hx : x âˆˆ e.source) : Function.Surjective (mfderiv I I' e x) :=
  (he.mfderiv hx).surjective
#align local_homeomorph.mdifferentiable.mfderiv_surjective PartialHomeomorph.MDifferentiable.mfderiv_surjective

theorem ker_mfderiv_eq_bot {x : M} (hx : x âˆˆ e.source) : LinearMap.ker (mfderiv I I' e x) = âŠ¥ :=
  (he.mfderiv hx).toLinearEquiv.ker
#align local_homeomorph.mdifferentiable.ker_mfderiv_eq_bot PartialHomeomorph.MDifferentiable.ker_mfderiv_eq_bot

theorem range_mfderiv_eq_top {x : M} (hx : x âˆˆ e.source) : LinearMap.range (mfderiv I I' e x) = âŠ¤ :=
  (he.mfderiv hx).toLinearEquiv.range
#align local_homeomorph.mdifferentiable.range_mfderiv_eq_top PartialHomeomorph.MDifferentiable.range_mfderiv_eq_top

theorem range_mfderiv_eq_univ {x : M} (hx : x âˆˆ e.source) : range (mfderiv I I' e x) = univ :=
  (he.mfderiv_surjective hx).range_eq
#align local_homeomorph.mdifferentiable.range_mfderiv_eq_univ PartialHomeomorph.MDifferentiable.range_mfderiv_eq_univ

theorem trans (he' : e'.MDifferentiable I' I'') : (e.trans e').MDifferentiable I I'' := by
  constructor
  Â· intro x hx
    simp only [mfld_simps] at hx
    exact
      ((he'.mdifferentiableAt hx.2).comp _ (he.mdifferentiableAt hx.1)).mdifferentiableWithinAt
  Â· intro x hx
    simp only [mfld_simps] at hx
    exact
      ((he.symm.mdifferentiableAt hx.2).comp _
          (he'.symm.mdifferentiableAt hx.1)).mdifferentiableWithinAt
#align local_homeomorph.mdifferentiable.trans PartialHomeomorph.MDifferentiable.trans

end PartialHomeomorph.MDifferentiable

/-! ### Differentiability of `extChartAt` -/

section extChartAt

variable {ð•œ : Type*} [NontriviallyNormedField ð•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ð•œ E] {H : Type*} [TopologicalSpace H] (I : ModelWithCorners ð•œ E H) {M : Type*}
  [TopologicalSpace M] [ChartedSpace H M] [SmoothManifoldWithCorners I M] {s : Set M} {x y : M}

theorem hasMFDerivAt_extChartAt (h : y âˆˆ (chartAt H x).source) :
    HasMFDerivAt I ð“˜(ð•œ, E) (extChartAt I x) y (mfderiv I I (chartAt H x) y : _) :=
  I.hasMFDerivAt.comp y ((mdifferentiable_chart I x).mdifferentiableAt h).hasMFDerivAt
#align has_mfderiv_at_ext_chart_at hasMFDerivAt_extChartAt

theorem hasMFDerivWithinAt_extChartAt (h : y âˆˆ (chartAt H x).source) :
    HasMFDerivWithinAt I ð“˜(ð•œ, E) (extChartAt I x) s y (mfderiv I I (chartAt H x) y : _) :=
  (hasMFDerivAt_extChartAt I h).hasMFDerivWithinAt
#align has_mfderiv_within_at_ext_chart_at hasMFDerivWithinAt_extChartAt

theorem mdifferentiableAt_extChartAt (h : y âˆˆ (chartAt H x).source) :
    MDifferentiableAt I ð“˜(ð•œ, E) (extChartAt I x) y :=
  (hasMFDerivAt_extChartAt I h).mdifferentiableAt
#align mdifferentiable_at_ext_chart_at mdifferentiableAt_extChartAt

theorem mdifferentiableOn_extChartAt :
    MDifferentiableOn I ð“˜(ð•œ, E) (extChartAt I x) (chartAt H x).source := fun _y hy =>
  (hasMFDerivWithinAt_extChartAt I hy).mdifferentiableWithinAt
#align mdifferentiable_on_ext_chart_at mdifferentiableOn_extChartAt

end extChartAt
