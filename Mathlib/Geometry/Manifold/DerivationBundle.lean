/-
Copyright (c) 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri
-/
import Mathlib.Geometry.Manifold.Algebra.SmoothFunctions
import Mathlib.RingTheory.Derivation.Basic

/-!

# Derivation bundle

In this file we define the derivations at a point of a manifold on the algebra of smooth functions.
Moreover, we define the differential of a function in terms of derivations.

The content of this file is not meant to be regarded as an alternative definition to the current
tangent bundle but rather as a purely algebraic theory that provides a purely algebraic definition
of the Lie algebra for a Lie group. This theory coincides with the usual tangent bundle in the
case of finite-dimensional `C^âˆ` real manifolds, but not in the general case.
-/


variable (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H) (M : Type*)
  [TopologicalSpace M] [ChartedSpace H M] (n : WithTop â„•âˆ)

open scoped Manifold ContDiff

-- the following two instances prevent poorly understood type class inference timeout problems
instance smoothFunctionsAlgebra : Algebra ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ := by infer_instance

instance smooth_functions_tower : IsScalarTower ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ C^âˆâŸ®I, M; ğ•œâŸ¯ := by infer_instance

/-- Type synonym, introduced to put a different `SMul` action on `C^nâŸ®I, M; ğ•œâŸ¯`
which is defined as `f â€¢ r = f(x) * r`.
Denoted as `C^nâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ©` within the `Derivation` namespace. -/
@[nolint unusedArguments]
def PointedContMDiffMap (_ : M) :=
  C^nâŸ®I, M; ğ•œâŸ¯

@[inherit_doc]
scoped[Derivation] notation "C^" n "âŸ®" I ", " M "; " ğ•œ "âŸ¯âŸ¨" x "âŸ©" => PointedContMDiffMap ğ•œ I M n x

variable {ğ•œ M}

namespace PointedContMDiffMap

open scoped Derivation

instance instFunLike {x : M} : FunLike C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© M ğ•œ :=
  ContMDiffMap.instFunLike

instance {x : M} : CommRing C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© :=
  ContMDiffMap.commRing

instance {x : M} : Algebra ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© :=
  ContMDiffMap.algebra

instance {x : M} : Inhabited C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© :=
  âŸ¨0âŸ©

instance {x : M} : Algebra C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© C^âˆâŸ®I, M; ğ•œâŸ¯ :=
  Algebra.id C^âˆâŸ®I, M; ğ•œâŸ¯

instance {x : M} : IsScalarTower ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© C^âˆâŸ®I, M; ğ•œâŸ¯ :=
  IsScalarTower.right

variable {I}

/-- `ContMDiffMap.evalRingHom` gives rise to an algebra structure of `C^âˆâŸ®I, M; ğ•œâŸ¯` on `ğ•œ`. -/
instance evalAlgebra {x : M} : Algebra C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© ğ•œ :=
  (ContMDiffMap.evalRingHom x : C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© â†’+* ğ•œ).toAlgebra

/-- With the `evalAlgebra` algebra structure evaluation is actually an algebra morphism. -/
def eval (x : M) : C^âˆâŸ®I, M; ğ•œâŸ¯ â†’â‚[C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ©] ğ•œ :=
  Algebra.ofId C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© ğ•œ

theorem smul_def (x : M) (f : C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ©) (k : ğ•œ) : f â€¢ k = f x * k :=
  rfl

instance (x : M) : IsScalarTower ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© ğ•œ where
  smul_assoc k f h := by
    rw [smul_def, smul_def, ContMDiffMap.coe_smul, Pi.smul_apply, smul_eq_mul, smul_eq_mul,
      mul_assoc]

end PointedContMDiffMap

open scoped Derivation

/-- The derivations at a point of a manifold. Some regard this as a possible definition of the
tangent space, as this coincides with the usual tangent space for finite-dimensional `C^âˆ` real
manifolds. The identification is not true in general, though. -/
abbrev PointDerivation (x : M) :=
  Derivation ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© ğ•œ

section

open scoped Derivation

variable (X : Derivation ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ C^âˆâŸ®I, M; ğ•œâŸ¯) (f : C^âˆâŸ®I, M; ğ•œâŸ¯)

/-- Evaluation at a point gives rise to a `C^âˆâŸ®I, M; ğ•œâŸ¯`-linear map between `C^âˆâŸ®I, M; ğ•œâŸ¯` and `ğ•œ`.
-/
def ContMDiffFunction.evalAt (x : M) : C^âˆâŸ®I, M; ğ•œâŸ¯ â†’â‚—[C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ©] ğ•œ :=
  (PointedContMDiffMap.eval x).toLinearMap

namespace Derivation

variable {I}

/-- The evaluation at a point as a linear map. -/
def evalAt (x : M) : Derivation ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ C^âˆâŸ®I, M; ğ•œâŸ¯ â†’â‚—[ğ•œ] PointDerivation I x :=
  (ContMDiffFunction.evalAt I x).compDer

theorem evalAt_apply (x : M) : evalAt x X f = (X f) x :=
  rfl

end Derivation

variable {I} {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E'] {H' : Type*}
  [TopologicalSpace H'] {I' : ModelWithCorners ğ•œ E' H'} {M' : Type*} [TopologicalSpace M']
  [ChartedSpace H' M']

/-- The heterogeneous differential as a linear map, denoted as `ğ’…â‚•` within the `Manifold` namespace.
Instead of taking a function as an argument, this
differential takes `h : f x = y`. It is particularly handy for situations where the points
at which it has to be evaluated are equal but not definitionally equal. -/
def hfdifferential {f : C^âˆâŸ®I, M; I', M'âŸ¯} {x : M} {y : M'} (h : f x = y) :
    PointDerivation I x â†’â‚—[ğ•œ] PointDerivation I' y where
  toFun v :=
    Derivation.mk'
      { toFun := fun g => v (g.comp f)
        map_add' := fun g g' => by rw [ContMDiffMap.add_comp, Derivation.map_add]
        map_smul' := fun k g => by
          dsimp; rw [ContMDiffMap.smul_comp, Derivation.map_smul, smul_eq_mul] }
      fun g g' => by
        dsimp
        rw [ContMDiffMap.mul_comp, Derivation.leibniz,
          PointedContMDiffMap.smul_def, ContMDiffMap.comp_apply,
          PointedContMDiffMap.smul_def, ContMDiffMap.comp_apply, h]
        norm_cast
  map_smul' _ _ := rfl
  map_add' _ _ := rfl

/-- The homogeneous differential as a linear map, denoted as `ğ’…` within the `Manifold` namespace. -/
def fdifferential (f : C^âˆâŸ®I, M; I', M'âŸ¯) (x : M) :
    PointDerivation I x â†’â‚—[ğ•œ] PointDerivation I' (f x) :=
  hfdifferential (rfl : f x = f x)

-- Standard notation for the differential. The abbreviation is `MId`.
@[inherit_doc] scoped[Manifold] notation "ğ’…" => fdifferential

-- Standard notation for the differential. The abbreviation is `MId`.
@[inherit_doc] scoped[Manifold] notation "ğ’…â‚•" => hfdifferential

@[simp]
theorem fdifferential_apply (f : C^âˆâŸ®I, M; I', M'âŸ¯) {x : M} (v : PointDerivation I x)
    (g : C^âˆâŸ®I', M'; ğ•œâŸ¯) : ğ’… f x v g = v (g.comp f) :=
  rfl
@[simp]
theorem hfdifferential_apply {f : C^âˆâŸ®I, M; I', M'âŸ¯} {x : M} {y : M'} (h : f x = y)
    (v : PointDerivation I x) (g : C^âˆâŸ®I', M'; ğ•œâŸ¯) : ğ’…â‚• h v g = ğ’… f x v g :=
  rfl
variable {E'' : Type*} [NormedAddCommGroup E''] [NormedSpace ğ•œ E''] {H'' : Type*}
  [TopologicalSpace H''] {I'' : ModelWithCorners ğ•œ E'' H''} {M'' : Type*} [TopologicalSpace M'']
  [ChartedSpace H'' M'']

@[simp]
theorem fdifferential_comp (g : C^âˆâŸ®I', M'; I'', M''âŸ¯) (f : C^âˆâŸ®I, M; I', M'âŸ¯) (x : M) :
    ğ’… (g.comp f) x = (ğ’… g (f x)).comp (ğ’… f x) :=
  rfl

end
