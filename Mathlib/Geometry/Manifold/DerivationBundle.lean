/-
Copyright (c) 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri
-/
import Mathlib.Geometry.Manifold.Algebra.SmoothFunctions
import Mathlib.RingTheory.Derivation.Basic

#align_import geometry.manifold.derivation_bundle from "leanprover-community/mathlib"@"86c29aefdba50b3f33e86e52e3b2f51a0d8f0282"

/-!

# Derivation bundle

In this file we define the derivations at a point of a manifold on the algebra of smooth fuctions.
Moreover, we define the differential of a function in terms of derivations.

The content of this file is not meant to be regarded as an alternative definition to the current
tangent bundle but rather as a purely algebraic theory that provides a purely algebraic definition
of the Lie algebra for a Lie group.

-/


variable (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H) (M : Type*)
  [TopologicalSpace M] [ChartedSpace H M] (n : â„•âˆ)

open scoped Manifold

-- the following two instances prevent poorly understood type class inference timeout problems
instance smoothFunctionsAlgebra : Algebra ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ := by infer_instance
#align smooth_functions_algebra smoothFunctionsAlgebra

instance smooth_functions_tower : IsScalarTower ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ C^âˆâŸ®I, M; ğ•œâŸ¯ := by infer_instance
#align smooth_functions_tower smooth_functions_tower

/-- Type synonym, introduced to put a different `SMul` action on `C^nâŸ®I, M; ğ•œâŸ¯`
which is defined as `f â€¢ r = f(x) * r`. -/
@[nolint unusedArguments]
def PointedSmoothMap (_ : M) :=
  C^nâŸ®I, M; ğ•œâŸ¯
#align pointed_smooth_map PointedSmoothMap

@[inherit_doc]
scoped[Derivation] notation "C^" n "âŸ®" I ", " M "; " ğ•œ "âŸ¯âŸ¨" x "âŸ©" => PointedSmoothMap ğ•œ I M n x

variable {ğ•œ M}

namespace PointedSmoothMap

open scoped Derivation

instance instFunLike {x : M} : FunLike C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© M ğ•œ :=
  ContMDiffMap.instFunLike
#align pointed_smooth_map.fun_like PointedSmoothMap.instFunLike

instance {x : M} : CommRing C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© :=
  SmoothMap.commRing

instance {x : M} : Algebra ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© :=
  SmoothMap.algebra

instance {x : M} : Inhabited C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© :=
  âŸ¨0âŸ©

instance {x : M} : Algebra C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© C^âˆâŸ®I, M; ğ•œâŸ¯ :=
  Algebra.id C^âˆâŸ®I, M; ğ•œâŸ¯

instance {x : M} : IsScalarTower ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© C^âˆâŸ®I, M; ğ•œâŸ¯ :=
  IsScalarTower.right

variable {I}

/-- `SmoothMap.evalRingHom` gives rise to an algebra structure of `C^âˆâŸ®I, M; ğ•œâŸ¯` on `ğ•œ`. -/
instance evalAlgebra {x : M} : Algebra C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© ğ•œ :=
  (SmoothMap.evalRingHom x : C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© â†’+* ğ•œ).toAlgebra
#align pointed_smooth_map.eval_algebra PointedSmoothMap.evalAlgebra

/-- With the `evalAlgebra` algebra structure evaluation is actually an algebra morphism. -/
def eval (x : M) : C^âˆâŸ®I, M; ğ•œâŸ¯ â†’â‚[C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ©] ğ•œ :=
  Algebra.ofId C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© ğ•œ
#align pointed_smooth_map.eval PointedSmoothMap.eval

theorem smul_def (x : M) (f : C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ©) (k : ğ•œ) : f â€¢ k = f x * k :=
  rfl
#align pointed_smooth_map.smul_def PointedSmoothMap.smul_def

instance (x : M) : IsScalarTower ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© ğ•œ where
  smul_assoc k f h := by
    rw [smul_def, smul_def, SmoothMap.coe_smul, Pi.smul_apply, smul_eq_mul, smul_eq_mul, mul_assoc]

end PointedSmoothMap

open scoped Derivation

/-- The derivations at a point of a manifold. Some regard this as a possible definition of the
tangent space -/
abbrev PointDerivation (x : M) :=
  Derivation ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© ğ•œ
#align point_derivation PointDerivation

section

open scoped Derivation

variable (X Y : Derivation ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ C^âˆâŸ®I, M; ğ•œâŸ¯) (f g : C^âˆâŸ®I, M; ğ•œâŸ¯) (r : ğ•œ)

/-- Evaluation at a point gives rise to a `C^âˆâŸ®I, M; ğ•œâŸ¯`-linear map between `C^âˆâŸ®I, M; ğ•œâŸ¯` and `ğ•œ`.
 -/
def SmoothFunction.evalAt (x : M) : C^âˆâŸ®I, M; ğ•œâŸ¯ â†’â‚—[C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ©] ğ•œ :=
  (PointedSmoothMap.eval x).toLinearMap
#align smooth_function.eval_at SmoothFunction.evalAt

namespace Derivation

variable {I}

/-- The evaluation at a point as a linear map. -/
def evalAt (x : M) : Derivation ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ C^âˆâŸ®I, M; ğ•œâŸ¯ â†’â‚—[ğ•œ] PointDerivation I x :=
  (SmoothFunction.evalAt I x).compDer
#align derivation.eval_at Derivation.evalAt

theorem evalAt_apply (x : M) : evalAt x X f = (X f) x :=
  rfl
#align derivation.eval_at_apply Derivation.evalAt_apply

end Derivation

variable {I} {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E'] {H' : Type*}
  [TopologicalSpace H'] {I' : ModelWithCorners ğ•œ E' H'} {M' : Type*} [TopologicalSpace M']
  [ChartedSpace H' M']

/-- The heterogeneous differential as a linear map. Instead of taking a function as an argument this
differential takes `h : f x = y`. It is particularly handy to deal with situations where the points
on where it has to be evaluated are equal but not definitionally equal. -/
def hfdifferential {f : C^âˆâŸ®I, M; I', M'âŸ¯} {x : M} {y : M'} (h : f x = y) :
    PointDerivation I x â†’â‚—[ğ•œ] PointDerivation I' y where
  toFun v :=
    Derivation.mk'
      { toFun := fun g => v (g.comp f)
        map_add' := fun g g' => by dsimp; rw [SmoothMap.add_comp, Derivation.map_add]
        map_smul' := fun k g => by
          dsimp; rw [SmoothMap.smul_comp, Derivation.map_smul, smul_eq_mul] }
      fun g g' => by
        dsimp
        rw [SmoothMap.mul_comp, Derivation.leibniz,
          PointedSmoothMap.smul_def, ContMDiffMap.comp_apply,
          PointedSmoothMap.smul_def, ContMDiffMap.comp_apply, h]
        norm_cast
  map_smul' k v := rfl
  map_add' v w := rfl
#align hfdifferential hfdifferential

/-- The homogeneous differential as a linear map. -/
def fdifferential (f : C^âˆâŸ®I, M; I', M'âŸ¯) (x : M) :
    PointDerivation I x â†’â‚—[ğ•œ] PointDerivation I' (f x) :=
  hfdifferential (rfl : f x = f x)
#align fdifferential fdifferential

-- Standard notation for the differential. The abbreviation is `MId`.
scoped[Manifold] notation "ğ’…" => fdifferential

-- Standard notation for the differential. The abbreviation is `MId`.
scoped[Manifold] notation "ğ’…â‚•" => hfdifferential

@[simp]
theorem apply_fdifferential (f : C^âˆâŸ®I, M; I', M'âŸ¯) {x : M} (v : PointDerivation I x)
    (g : C^âˆâŸ®I', M'; ğ•œâŸ¯) : ğ’… f x v g = v (g.comp f) :=
  rfl
#align apply_fdifferential apply_fdifferential

@[simp]
theorem apply_hfdifferential {f : C^âˆâŸ®I, M; I', M'âŸ¯} {x : M} {y : M'} (h : f x = y)
    (v : PointDerivation I x) (g : C^âˆâŸ®I', M'; ğ•œâŸ¯) : ğ’…â‚• h v g = ğ’… f x v g :=
  rfl
#align apply_hfdifferential apply_hfdifferential

variable {E'' : Type*} [NormedAddCommGroup E''] [NormedSpace ğ•œ E''] {H'' : Type*}
  [TopologicalSpace H''] {I'' : ModelWithCorners ğ•œ E'' H''} {M'' : Type*} [TopologicalSpace M'']
  [ChartedSpace H'' M'']

@[simp]
theorem fdifferential_comp (g : C^âˆâŸ®I', M'; I'', M''âŸ¯) (f : C^âˆâŸ®I, M; I', M'âŸ¯) (x : M) :
    ğ’… (g.comp f) x = (ğ’… g (f x)).comp (ğ’… f x) :=
  rfl
#align fdifferential_comp fdifferential_comp

end
