/-
Copyright (c) 2024 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Algebra.Lie.Basic
import Mathlib.Geometry.Manifold.Algebra.LieGroup
import Mathlib.Geometry.Manifold.VectorField

/-!
# The Lie algebra of a Lie group

Given a Lie group, we define `LieGroupAlgebra I G` as its tangent space at the identity, and we
endow it with a Lie bracket, as follows. Given two vectors `v, w : LieGroupAlgebra I G`, consider
the associated left-invariant vector fields `invariantVectorField v` (given at a point `g` by
the image of `v` under the derivative of left-multiplication by `g`) and `invariantVectorField w`.
Then take their Lie bracket at the identity: this is by definition the bracket of `v` and `w`.

Due to general properties of the Lie bracket of vector fields, this gives a Lie algebra structure
on `LieGroupAlgebra I G`.

Note that one can also define a Lie algebra on the space of left-invariant derivations on `C^‚àû`
functions (see `LeftInvariantDerivation.instLieAlgebra`). For finite-dimensional `C^‚àû` real
manifolds, this space of derivations can be canonically identified with the tangent space, and we
recover the same Lie algebra structure (TODO: prove this). In other smoothness classes or on other
fields, this identification is not always true, though, so the derivations point of view does not
work in these settings. Therefore, the point of view in the current file is more general, and
should be favored when possible.
-/

noncomputable section

section LieGroup

open Bundle Filter Function Set
open scoped Manifold

variable {ùïú : Type*} [NontriviallyNormedField ùïú]
  {H : Type*} [TopologicalSpace H] {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {I : ModelWithCorners ùïú E H}
  {G : Type*} [TopologicalSpace G] [ChartedSpace H G] [Group G]

variable (I G) in
/-- The Lie algebra of a Lie group, i.e., its tangent space at the identity. We use the word
`LieGroupAlgebra` instead of `LieAlgebra` as the latter is taken as a generic class. -/
abbrev LieGroupAlgebra : Type _ := TangentSpace I (1 : G)

/-- The invariant vector field associated to a vector in the Lie alebra. -/
def invariantVectorField (v : LieGroupAlgebra I G) (g : G) : TangentSpace I g :=
  mfderiv I I (g * ¬∑) (1 : G) v

lemma invariantVectorField_add (v w : LieGroupAlgebra I G) :
    invariantVectorField (v + w) = invariantVectorField v + invariantVectorField w := by
  ext g
  simp [invariantVectorField]

lemma invariantVectorField_smul (c : ùïú) (v : LieGroupAlgebra I G) :
    invariantVectorField (c ‚Ä¢ v) = c ‚Ä¢ invariantVectorField v := by
  ext g
  simp [invariantVectorField]

open VectorField

variable [LieGroup I G]

@[simp]
lemma inverse_mfderiv_mul_left {g h : G} :
    (mfderiv I I (fun b ‚Ü¶ g * b) h).inverse = mfderiv I I (fun b ‚Ü¶ g‚Åª¬π * b) (g * h) := by
  have A : mfderiv I I ((fun x ‚Ü¶ g‚Åª¬π * x) ‚àò (fun x ‚Ü¶ g * x)) h =
      ContinuousLinearMap.id _ _ := by
    have : (fun x ‚Ü¶ g‚Åª¬π * x) ‚àò (fun x ‚Ü¶ g * x) = id := by ext x; simp
    rw [this, id_eq, mfderiv_id]
  rw [mfderiv_comp (I' := I) _ (contMDiff_mul_left.contMDiffAt.mdifferentiableAt le_top)
    (contMDiff_mul_left.contMDiffAt.mdifferentiableAt le_top)] at A
  have A' : mfderiv I I ((fun x ‚Ü¶ g * x) ‚àò (fun x ‚Ü¶ g‚Åª¬π * x)) (g * h) =
      ContinuousLinearMap.id _ _ := by
    have : (fun x ‚Ü¶ g * x) ‚àò (fun x ‚Ü¶ g‚Åª¬π * x) = id := by ext x; simp
    rw [this, id_eq, mfderiv_id]
  rw [mfderiv_comp (I' := I) _ (contMDiff_mul_left.contMDiffAt.mdifferentiableAt le_top)
    (contMDiff_mul_left.contMDiffAt.mdifferentiableAt le_top), inv_mul_cancel_left g h] at A'
  exact ContinuousLinearMap.inverse_eq A' A

lemma mpullback_invariantVectorField (g : G) (v : LieGroupAlgebra I G) :
    mpullback I I (g * ¬∑) (invariantVectorField v) = invariantVectorField v := by
  ext h
  simp only [mpullback, inverse_mfderiv_mul_left, invariantVectorField]
  have D : (fun x ‚Ü¶ h * x) = (fun b ‚Ü¶ g‚Åª¬π * b) ‚àò (fun x ‚Ü¶ g * h * x) := by
    ext x; simp only [comp_apply]; group
  rw [D, mfderiv_comp (I' := I)]
  ¬∑ congr 2
    simp
  ¬∑ exact contMDiff_mul_left.contMDiffAt.mdifferentiableAt le_top
  ¬∑ exact contMDiff_mul_left.contMDiffAt.mdifferentiableAt le_top

lemma invariantVectorField_eq_mpullback (g : G) (V : Œ† (g : G), TangentSpace I g) :
    invariantVectorField (V 1) g = mpullback I I (g ‚Åª¬π * ¬∑) V g := by
  have A : 1 = g‚Åª¬π * g := by simp
  simp [invariantVectorField, mpullback]
  congr
  simp

theorem contMDiff_invariantVectorField (v : LieGroupAlgebra I G) {n : ‚Ñï‚àû} :
    ContMDiff I I.tangent n
      (fun (g : G) ‚Ü¶ (invariantVectorField v g : TangentBundle I G)) := by
  apply ContMDiff.of_le _ le_top
  let fg : G ‚Üí TangentBundle I G := fun g ‚Ü¶ TotalSpace.mk' E g 0
  have sfg : ContMDiff I I.tangent ‚ä§ fg := contMDiff_zeroSection _ _
  let fv : G ‚Üí TangentBundle I G := fun _ ‚Ü¶ TotalSpace.mk' E 1 v
  have sfv : ContMDiff I I.tangent ‚ä§ fv := contMDiff_const
  let F‚ÇÅ : G ‚Üí (TangentBundle I G √ó TangentBundle I G) := fun g ‚Ü¶ (fg g, fv g)
  have S‚ÇÅ : ContMDiff I (I.tangent.prod I.tangent) ‚ä§ F‚ÇÅ := ContMDiff.prod_mk sfg sfv
  let F‚ÇÇ : (TangentBundle I G √ó TangentBundle I G) ‚Üí TangentBundle (I.prod I) (G √ó G) :=
    (equivTangentBundleProd I G I G).symm
  have S‚ÇÇ : ContMDiff (I.tangent.prod I.tangent) (I.prod I).tangent ‚ä§ F‚ÇÇ :=
    smooth_equivTangentBundleProd_symm
  let F‚ÇÉ : TangentBundle (I.prod I) (G √ó G) ‚Üí TangentBundle I G :=
    tangentMap (I.prod I) I (fun (p : G √ó G) ‚Ü¶ p.1 * p.2)
  have S‚ÇÉ : ContMDiff (I.prod I).tangent I.tangent ‚ä§ F‚ÇÉ := by
    apply ContMDiff.contMDiff_tangentMap _ (m := ‚ä§) le_rfl
    exact contMDiff_mul I (G := G)
  let S := (S‚ÇÉ.comp S‚ÇÇ).comp S‚ÇÅ
  convert S with g
  ¬∑ simp [F‚ÇÅ, F‚ÇÇ, F‚ÇÉ]
  ¬∑ simp only [comp_apply, tangentMap, F‚ÇÉ, F‚ÇÇ, F‚ÇÅ]
    rw [mfderiv_prod_eq_add_apply ((contMDiff_mul I (G := G)).mdifferentiableAt le_top)]
    simp [invariantVectorField]

theorem contMDiffAt_invariantVectorField (v : LieGroupAlgebra I G) {n : ‚Ñï‚àû} {g : G }:
    ContMDiffAt I I.tangent n
      (fun (g : G) ‚Ü¶ (invariantVectorField v g : TangentBundle I G)) g :=
  (contMDiff_invariantVectorField v).contMDiffAt

theorem mdifferentiable_invariantVectorField (v : LieGroupAlgebra I G) :
    MDifferentiable I I.tangent
      (fun (g : G) ‚Ü¶ (invariantVectorField v g : TangentBundle I G)) :=
  (contMDiff_invariantVectorField v).mdifferentiable le_rfl

theorem mdifferentiableAt_invariantVectorField (v : LieGroupAlgebra I G) {g : G} :
    MDifferentiableAt I I.tangent
      (fun (g : G) ‚Ü¶ (invariantVectorField v g : TangentBundle I G)) g :=
  (contMDiffAt_invariantVectorField v).mdifferentiableAt le_rfl

open VectorField

instance : Bracket (LieGroupAlgebra I G) (LieGroupAlgebra I G) where
  bracket v w := mlieBracket I (invariantVectorField v) (invariantVectorField w) (1 : G)

omit [LieGroup I G] in
lemma bracket_def (v w : LieGroupAlgebra I G) :
    ‚ÅÖv, w‚ÅÜ = mlieBracket I (invariantVectorField v) (invariantVectorField w) (1 : G) := rfl

variable [IsRCLikeNormedField ùïú] [CompleteSpace E]

lemma invariantVector_mlieBracket (v w : LieGroupAlgebra I G) :
    invariantVectorField (mlieBracket I (invariantVectorField v) (invariantVectorField w) 1) =
    mlieBracket I (invariantVectorField v) (invariantVectorField w) := by
  ext g
  rw [invariantVectorField_eq_mpullback, mpullback_mlieBracket, mpullback_invariantVectorField,
    mpullback_invariantVectorField]
  ¬∑ exact mdifferentiableAt_invariantVectorField _
  ¬∑ exact mdifferentiableAt_invariantVectorField _
  ¬∑ exact contMDiffAt_mul_left

instance : LieRing (LieGroupAlgebra I G) where
  add_lie u v w := by
    simp only [bracket_def, invariantVectorField_add]
    rw [mlieBracket_add_left]
    ¬∑ exact mdifferentiableAt_invariantVectorField _
    ¬∑ exact mdifferentiableAt_invariantVectorField _
  lie_add u v w := by
    simp only [bracket_def, invariantVectorField_add]
    rw [mlieBracket_add_right]
    ¬∑ exact mdifferentiableAt_invariantVectorField _
    ¬∑ exact mdifferentiableAt_invariantVectorField _
  lie_self v := by simp [bracket_def]
  leibniz_lie u v w := by
    simp [bracket_def, invariantVector_mlieBracket]
    apply leibniz_identity_mlieBracket_apply <;>
      exact contMDiff_invariantVectorField _ _

instance : LieAlgebra ùïú (LieGroupAlgebra I G) where
  lie_smul c v w := by
    simp only [bracket_def, invariantVectorField_smul]
    rw [mlieBracket_smul_right]
    exact mdifferentiableAt_invariantVectorField _

end LieGroup
