/-
Copyright (c) 2024 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Geometry.Manifold.VectorField
import Mathlib.Algebra.Lie.Basic

/-!
# The Lie algebra of a Lie group

Given a Lie group, we define `LieGroupAlgebra I G` as its tangent space at the identity, and we
endow it with a Lie bracket, as follows. Given two vectors `v, w : LieGroupAlgebra I G`, consider
the associated left-invariant vector fields `invariantVectorField v` (given at a point `g` by
the image of `v` under the derivative of left-multiplication by `g`) and `invariantVectorField w`.
Then take their Lie bracket at the identity: this is by definition the bracket of `v` and `w`.

Due to general properties of the Lie bracket of vector fields, this gives a Lie algebra structure
on `LieGroupAlgebra I G`.
-/

noncomputable section

section LieGroup

open Bundle Filter Function Set
open scoped Manifold

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {H : Type*} [TopologicalSpace H] {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {I : ModelWithCorners ğ•œ E H}
  {G : Type*} [TopologicalSpace G] [ChartedSpace H G] [Group G]

variable (I G) in
/-- The Lie algebra of a Lie group, i.e., its tangent space at the identity. We use the word
`LieGroupAlgebra` instead of `LieAlgebra` as the latter is taken as a generic class. -/
abbrev LieGroupAlgebra : Type _ := TangentSpace I (1 : G)

/-- The invariant vector field associated to a vector in the Lie alebra. -/
def invariantVectorField (v : LieGroupAlgebra I G) (g : G) : TangentSpace I g :=
  mfderiv I I (g * Â·) (1 : G) v

lemma invariantVectorField_add (v w : LieGroupAlgebra I G) :
    invariantVectorField (v + w) = invariantVectorField v + invariantVectorField w := by
  ext g
  simp [invariantVectorField]

lemma invariantVectorField_smul (c : ğ•œ) (v : LieGroupAlgebra I G) :
    invariantVectorField (c â€¢ v) = c â€¢ invariantVectorField v := by
  ext g
  simp [invariantVectorField]

open VectorField

variable [LieGroup I G]

@[simp]
lemma inverse_mfderiv_mul_left {g h : G} :
    (mfderiv I I (fun b â†¦ g * b) h).inverse = mfderiv I I (fun b â†¦ gâ»Â¹ * b) (g * h) := by
  have A : mfderiv I I ((fun x â†¦ gâ»Â¹ * x) âˆ˜ (fun x â†¦ g * x)) h =
      ContinuousLinearMap.id _ _ := by
    have : (fun x â†¦ gâ»Â¹ * x) âˆ˜ (fun x â†¦ g * x) = id := by ext x; simp
    rw [this, id_eq, mfderiv_id]
  rw [mfderiv_comp (I' := I) _ smooth_mul_left.smoothAt.mdifferentiableAt
    smooth_mul_left.smoothAt.mdifferentiableAt] at A
  have A' : mfderiv I I ((fun x â†¦ g * x) âˆ˜ (fun x â†¦ gâ»Â¹ * x)) (g * h) =
      ContinuousLinearMap.id _ _ := by
    have : (fun x â†¦ g * x) âˆ˜ (fun x â†¦ gâ»Â¹ * x) = id := by ext x; simp
    rw [this, id_eq, mfderiv_id]
  rw [mfderiv_comp (I' := I) _ smooth_mul_left.smoothAt.mdifferentiableAt
    smooth_mul_left.smoothAt.mdifferentiableAt, inv_mul_cancel_left g h] at A'
  exact ContinuousLinearMap.inverse_eq A' A

lemma mpullback_invariantVectorField (g : G) (v : LieGroupAlgebra I G) :
    mpullback I I (g * Â·) (invariantVectorField v) = invariantVectorField v := by
  ext h
  simp only [mpullback, inverse_mfderiv_mul_left, invariantVectorField]
  have D : (fun x â†¦ h * x) = (fun b â†¦ gâ»Â¹ * b) âˆ˜ (fun x â†¦ g * h * x) := by
    ext x; simp only [comp_apply]; group
  rw [D, mfderiv_comp (I' := I)]
  Â· congr 2
    simp
  Â· exact smooth_mul_left.smoothAt.mdifferentiableAt
  Â· exact smooth_mul_left.smoothAt.mdifferentiableAt

lemma invariantVectorField_eq_mpullback (g : G) (V : Î  (g : G), TangentSpace I g) :
    invariantVectorField (V 1) g = mpullback I I (g â»Â¹ * Â·) V g := by
  have A : 1 = gâ»Â¹ * g := by simp
  simp [invariantVectorField, mpullback]
  congr
  simp

theorem contMDiff_invariantVectorField (v : LieGroupAlgebra I G) {n : â„•âˆ} :
    ContMDiff I I.tangent n
      (fun (g : G) â†¦ (invariantVectorField v g : TangentBundle I G)) := by
  apply ContMDiff.of_le _ le_top
  let fg : G â†’ TangentBundle I G := fun g â†¦ TotalSpace.mk' E g 0
  have sfg : Smooth I I.tangent fg := smooth_zeroSection _ _
  let fv : G â†’ TangentBundle I G := fun _ â†¦ TotalSpace.mk' E 1 v
  have sfv : Smooth I I.tangent fv := smooth_const
  let Fâ‚ : G â†’ (TangentBundle I G Ã— TangentBundle I G) := fun g â†¦ (fg g, fv g)
  have Sâ‚ : Smooth I (I.tangent.prod I.tangent) Fâ‚ := Smooth.prod_mk sfg sfv
  let Fâ‚‚ : (TangentBundle I G Ã— TangentBundle I G) â†’ TangentBundle (I.prod I) (G Ã— G) :=
    (equivTangentBundleProd I G I G).symm
  have Sâ‚‚ : Smooth (I.tangent.prod I.tangent) (I.prod I).tangent Fâ‚‚ :=
    smooth_equivTangentBundleProd_symm
  let Fâ‚ƒ : TangentBundle (I.prod I) (G Ã— G) â†’ TangentBundle I G :=
    tangentMap (I.prod I) I (fun (p : G Ã— G) â†¦ p.1 * p.2)
  have Sâ‚ƒ : Smooth (I.prod I).tangent I.tangent Fâ‚ƒ := by
    apply ContMDiff.contMDiff_tangentMap _ (m := âŠ¤) le_rfl
    exact smooth_mul I (G := G)
  let S := (Sâ‚ƒ.comp Sâ‚‚).comp Sâ‚
  convert S with g
  Â· simp [Fâ‚, Fâ‚‚, Fâ‚ƒ]
  Â· simp only [comp_apply, tangentMap, Fâ‚ƒ, Fâ‚‚, Fâ‚]
    rw [mfderiv_prod_eq_add_apply (smooth_mul I (G := G)).mdifferentiableAt]
    simp [invariantVectorField]

theorem contMDiffAt_invariantVectorField (v : LieGroupAlgebra I G) {n : â„•âˆ} {g : G }:
    ContMDiffAt I I.tangent n
      (fun (g : G) â†¦ (invariantVectorField v g : TangentBundle I G)) g :=
  (contMDiff_invariantVectorField v).contMDiffAt

theorem mdifferentiable_invariantVectorField (v : LieGroupAlgebra I G) :
    MDifferentiable I I.tangent
      (fun (g : G) â†¦ (invariantVectorField v g : TangentBundle I G)) :=
  (contMDiff_invariantVectorField v).mdifferentiable le_rfl

theorem mdifferentiableAt_invariantVectorField (v : LieGroupAlgebra I G) {g : G} :
    MDifferentiableAt I I.tangent
      (fun (g : G) â†¦ (invariantVectorField v g : TangentBundle I G)) g :=
  (contMDiffAt_invariantVectorField v).mdifferentiableAt le_rfl

open VectorField

instance : Bracket (LieGroupAlgebra I G) (LieGroupAlgebra I G) where
  bracket v w := mlieBracket I (invariantVectorField v) (invariantVectorField w) (1 : G)

omit [LieGroup I G] in
lemma bracket_def (v w : LieGroupAlgebra I G) :
    â…v, wâ† = mlieBracket I (invariantVectorField v) (invariantVectorField w) (1 : G) := rfl

variable [IsRCLikeNormedField ğ•œ] [CompleteSpace E]

lemma invariantVector_mlieBracket (v w : LieGroupAlgebra I G) :
    invariantVectorField (mlieBracket I (invariantVectorField v) (invariantVectorField w) 1) =
    mlieBracket I (invariantVectorField v) (invariantVectorField w) := by
  ext g
  rw [invariantVectorField_eq_mpullback, mpullback_mlieBracket, mpullback_invariantVectorField,
    mpullback_invariantVectorField]
  Â· exact mdifferentiableAt_invariantVectorField _
  Â· exact mdifferentiableAt_invariantVectorField _
  Â· exact contMDiffAt_mul_left

instance : LieRing (LieGroupAlgebra I G) where
  add_lie u v w := by
    simp only [bracket_def, invariantVectorField_add]
    rw [mlieBracket_add_left]
    Â· exact mdifferentiableAt_invariantVectorField _
    Â· exact mdifferentiableAt_invariantVectorField _
  lie_add u v w := by
    simp only [bracket_def, invariantVectorField_add]
    rw [mlieBracket_add_right]
    Â· exact mdifferentiableAt_invariantVectorField _
    Â· exact mdifferentiableAt_invariantVectorField _
  lie_self v := by simp [bracket_def]
  leibniz_lie u v w := by
    simp [bracket_def, invariantVector_mlieBracket]
    apply leibniz_identity_mlieBracket_apply <;>
      exact contMDiff_invariantVectorField _ _

instance : LieAlgebra ğ•œ (LieGroupAlgebra I G) where
  lie_smul c v w := by
    simp only [bracket_def, invariantVectorField_smul]
    rw [mlieBracket_smul_right]
    exact mdifferentiableAt_invariantVectorField _

end LieGroup
