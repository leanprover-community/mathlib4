/-
Copyright (c) 2024 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Geometry.Manifold.PoincareConjecture
import Mathlib.Geometry.Manifold.ContMDiffMFDeriv

/-!
# Glouglou

All this should probably be extended to `Within` versions, as we will need them when defining
things on manifolds possibly with boundary.

-/

noncomputable section

section LieBracketVectorField

variable (ğ•œ : Type*) [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G]

/-- The Lie bracket `[V, W] (x)` of two vector fields at a point, defined as
`DW(x) (V x) - DV(x) (W x)`. -/
def lieBracket (V W : E â†’ E) (x : E) : E :=
  fderiv ğ•œ W x (V x) - fderiv ğ•œ V x (W x)

lemma lieBracket_eq (V W : E â†’ E) :
    lieBracket ğ•œ V W = fun x â†¦ fderiv ğ•œ W x (V x) - fderiv ğ•œ V x (W x) := rfl

/-- The Lie derivative of a function with respect to a vector field `L_V f(x)`. This is just
`Df(x) (V x)`, but the notation emphasizes how this is linear in `f`.-/
def lieDeriv (V : E â†’ E) (f : E â†’ F) (x : E) : F := fderiv ğ•œ f x (V x)

lemma lieDeriv_eq (V : E â†’ E) (f : E â†’ F) : lieDeriv ğ•œ V f = fun x â†¦ fderiv ğ•œ f x (V x) := rfl

/-- The equation `L_V L_W f - L_W L_V f = L_{[V, W]} f`, which is the motivation for the definition
of the Lie bracket. This requires the second derivative of `f` to be symmetric. -/
lemma sub_eq_lieDeriv_lieBracket (V W : E â†’ E) (f : E â†’ F) (x : E)
    (hf : âˆ€ v w, fderiv ğ•œ (fderiv ğ•œ f) x v w = fderiv ğ•œ (fderiv ğ•œ f) x w v)
    (h'f : ContDiffAt ğ•œ 2 f x) (hV : DifferentiableAt ğ•œ V x) (hW : DifferentiableAt ğ•œ W x) :
    lieDeriv ğ•œ V (lieDeriv ğ•œ W f) x - lieDeriv ğ•œ W (lieDeriv ğ•œ V f) x =
      lieDeriv ğ•œ (lieBracket ğ•œ V W) f x := by
  have A : DifferentiableAt ğ•œ (fderiv ğ•œ f) x :=
    (h'f.fderiv_right (m := 1) le_rfl).differentiableAt le_rfl
  simp only [lieDeriv_eq, lieBracket_eq]
  rw [fderiv_clm_apply A hV, fderiv_clm_apply A hW]
  simp only [ContinuousLinearMap.add_apply, ContinuousLinearMap.coe_comp', Function.comp_apply,
    ContinuousLinearMap.flip_apply, map_sub, hf]
  abel

/-- The pullback of a vector field under a function, defined
as `(f^* V) (x) = Df(x)^{-1} (V (f x))`. If `Df(x)` is not invertible, we use the junk value `0`.
-/
def pullback (f : E â†’ F) (V : F â†’ F) (x : E) : E := (fderiv ğ•œ f x).inverse (V (f x))

variable {ğ•œ}

lemma pullback_eq_of_fderiv_eq
    {f : E â†’ F} {M : E â‰ƒL[ğ•œ] F} {x : E} (hf : M = fderiv ğ•œ f x) (V : F â†’ F) :
    pullback ğ•œ f V x = M.symm (V (f x)) := by simp [pullback, â† hf]

lemma pullback_eq_of_not_exists {f : E â†’ F} {x : E}
    (h : Â¬(âˆƒ M : E â‰ƒL[ğ•œ] F, M = fderiv ğ•œ f x)) (V : F â†’ F) :
    pullback ğ•œ f V x = 0 := by simp [pullback, h]

open scoped Topology Filter

theorem fderiv.comp'
    {f : E â†’ F} {g : F â†’ G} (x : E) (hg : DifferentiableAt ğ•œ g (f x))
    (hf : DifferentiableAt ğ•œ f x) :
    fderiv ğ•œ (fun y â†¦ g (f y)) x = (fderiv ğ•œ g (f x)).comp (fderiv ğ•œ f x) :=
  fderiv.comp x hg hf

lemma fderiv_pullback (f : E â†’ F) (V : F â†’ F) (x : E) (h'f : âˆƒ M : E â‰ƒL[ğ•œ] F, M = fderiv ğ•œ f x) :
    fderiv ğ•œ f x (pullback ğ•œ f V x) = V (f x) := by
  rcases h'f with âŸ¨M, hMâŸ©
  simp [pullback_eq_of_fderiv_eq hM, â† hM]

/-- The equation `L_{f^* V} (g âˆ˜ f) (x) = (L_V g) (f x)`, which is essentially the definition of
the pullback.
Note that `hf` can probably be removed, as it's implied by `h'f`.
-/
lemma lieDeriv_pullback (f : E â†’ F) (V : F â†’ F) (g : F â†’ G) (x : E)
    (hg : DifferentiableAt ğ•œ g (f x))
    (hf : DifferentiableAt ğ•œ f x) (h'f : âˆƒ M : E â‰ƒL[ğ•œ] F, M = fderiv ğ•œ f x) :
    lieDeriv ğ•œ (pullback ğ•œ f V) (g âˆ˜ f) x = lieDeriv ğ•œ V g (f x) := by
  rcases h'f with âŸ¨M, hMâŸ©
  rw [lieDeriv, lieDeriv, fderiv.comp _ hg hf]
  simp only [ContinuousLinearMap.coe_comp', Function.comp_apply]
  rw [fderiv_pullback]
  exact âŸ¨M, hMâŸ©

open Set

variable [CompleteSpace E]

/-- If a `C^2` map has an invertible derivative at a point, then nearby derivatives can be written
as continuous linear equivs, which depend in a `C^1` way on the point, as well as their inverse, and
moreover one can compute the derivative of the inverse. -/
lemma exists_continuousLinearEquiv_fderiv_symm_eq
    (f : E â†’ F) (x : E) (h'f : ContDiffAt ğ•œ 2 f x) (hf : âˆƒ M : E â‰ƒL[ğ•œ] F, M = fderiv ğ•œ f x) :
    âˆƒ N : E â†’ (E â‰ƒL[ğ•œ] F), ContDiffAt ğ•œ 1 (fun y â†¦ (N y : E â†’L[ğ•œ] F)) x
    âˆ§ ContDiffAt ğ•œ 1 (fun y â†¦ ((N y).symm : F â†’L[ğ•œ] E)) x
    âˆ§ (âˆ€á¶  y in ğ“ x, N y = fderiv ğ•œ f y)
    âˆ§ âˆ€ v, fderiv ğ•œ (fun y â†¦ ((N y).symm : F â†’L[ğ•œ] E)) x v
      = - (N x).symm  âˆ˜L ((fderiv ğ•œ (fderiv ğ•œ f) x v)) âˆ˜L (N x).symm := by
  classical
  rcases hf with âŸ¨M, hMâŸ©
  let U := {y | âˆƒ (N : E â‰ƒL[ğ•œ] F), N = fderiv ğ•œ f y}
  have hU : U âˆˆ ğ“ x := by
    have I : range ((â†‘) : (E â‰ƒL[ğ•œ] F) â†’ E â†’L[ğ•œ] F) âˆˆ ğ“ (fderiv ğ•œ f x) := by
      rw [â† hM]
      exact M.nhds
    have : ContinuousAt (fderiv ğ•œ f) x := (h'f.fderiv_right (m := 1) le_rfl).continuousAt
    exact this I
  let N : E â†’ (E â‰ƒL[ğ•œ] F) := fun x â†¦ if h : x âˆˆ U then h.choose else M
  have eN : (fun y â†¦ (N y : E â†’L[ğ•œ] F)) =á¶ [ğ“ x] fun y â†¦ fderiv ğ•œ f y := by
    filter_upwards [hU] with y hy
    simpa only [hy, â†“reduceDIte, N] using Exists.choose_spec hy
  have hN : ContDiffAt ğ•œ 1 (fun y â†¦ (N y : E â†’L[ğ•œ] F)) x := by
    have : ContDiffAt ğ•œ 1 (fun y â†¦ fderiv ğ•œ f y) x := h'f.fderiv_right (m := 1) (le_rfl)
    apply this.congr_of_eventuallyEq eN
  have hN' : ContDiffAt ğ•œ 1 (fun y â†¦ ((N y).symm : F â†’L[ğ•œ] E)) x := by
    have : ContDiffAt ğ•œ 1 (ContinuousLinearMap.inverse âˆ˜ (fun y â†¦ (N y : E â†’L[ğ•œ] F))) x :=
      (contDiffAt_map_inverse (N x)).comp x hN
    convert this with y
    simp only [Function.comp_apply, ContinuousLinearMap.inverse_equiv]
  refine âŸ¨N, hN, hN', eN, fun v â†¦ ?_âŸ©
  have A' y : ContinuousLinearMap.compL ğ•œ F E F (N y : E â†’L[ğ•œ] F) ((N y).symm : F â†’L[ğ•œ] E)
      = ContinuousLinearMap.id ğ•œ F := by ext; simp
  have : fderiv ğ•œ (fun y â†¦ ContinuousLinearMap.compL ğ•œ F E F (N y : E â†’L[ğ•œ] F)
      ((N y).symm : F â†’L[ğ•œ] E)) x v = 0 := by simp [A']
  have I : (N x : E â†’L[ğ•œ] F) âˆ˜L (fderiv ğ•œ (fun y â†¦ ((N y).symm : F â†’L[ğ•œ] E)) x v) =
      - (fderiv ğ•œ (fun y â†¦ (N y : E â†’L[ğ•œ] F)) x v) âˆ˜L ((N x).symm : F â†’L[ğ•œ] E) := by
    rw [ContinuousLinearMap.fderiv_of_bilinear _ (hN.differentiableAt le_rfl)
      (hN'.differentiableAt le_rfl)] at this
    simpa [eq_neg_iff_add_eq_zero] using this
  have B (M : F â†’L[ğ•œ] E) : M = ((N x).symm : F â†’L[ğ•œ] E) âˆ˜L ((N x) âˆ˜L M) := by
    ext; simp
  rw [B (fderiv ğ•œ (fun y â†¦ ((N y).symm : F â†’L[ğ•œ] E)) x v), I]
  simp only [ContinuousLinearMap.comp_neg, neg_inj, eN.fderiv_eq]

/-- The Lie bracket commutes with taking pullbacks. This requires the function to have symmetric
second derivative. -/
lemma lieBracket_pullback (f : E â†’ F) (V W : F â†’ F) (x : E)
    (hf : âˆ€ v w, fderiv ğ•œ (fderiv ğ•œ f) x v w = fderiv ğ•œ (fderiv ğ•œ f) x w v)
    (h'f : ContDiffAt ğ•œ 2 f x) (hV : DifferentiableAt ğ•œ V (f x)) (hW : DifferentiableAt ğ•œ W (f x)) :
    lieBracket ğ•œ (pullback ğ•œ f V) (pullback ğ•œ f W) x = pullback ğ•œ f (lieBracket ğ•œ V W) x := by
  by_cases h : âˆƒ M : E â‰ƒL[ğ•œ] F, M = fderiv ğ•œ f x; swap
  Â· simp [pullback_eq_of_not_exists h, lieBracket_eq]
  rcases exists_continuousLinearEquiv_fderiv_symm_eq f x h'f h
    with âŸ¨M, -, M_symm_smooth, hM, M_diffâŸ©
  have hMx : M x = fderiv ğ•œ f x := (mem_of_mem_nhds hM :)
  have AV : fderiv ğ•œ (pullback ğ•œ f V) x =
      fderiv ğ•œ (fun y â†¦ ((M y).symm : F â†’L[ğ•œ] E) (V (f y))) x := by
    apply Filter.EventuallyEq.fderiv_eq
    filter_upwards [hM] with y hy using pullback_eq_of_fderiv_eq hy _
  have AW : fderiv ğ•œ (pullback ğ•œ f W) x =
      fderiv ğ•œ (fun y â†¦ ((M y).symm : F â†’L[ğ•œ] E) (W (f y))) x := by
    apply Filter.EventuallyEq.fderiv_eq
    filter_upwards [hM] with y hy using pullback_eq_of_fderiv_eq hy _
  have Af : DifferentiableAt ğ•œ f x := h'f.differentiableAt one_le_two
  simp only [lieBracket_eq, pullback_eq_of_fderiv_eq hMx, map_sub, AV, AW]
  rw [fderiv_clm_apply, fderiv_clm_apply]
  Â· simp [fderiv.comp' x hW Af, â† hMx,
      fderiv.comp' x hV Af, M_diff, hf]
  Â· exact M_symm_smooth.differentiableAt le_rfl
  Â· exact hV.comp x Af
  Â· exact M_symm_smooth.differentiableAt le_rfl
  Â· exact hW.comp x Af

end LieBracketVectorField

open Bundle Filter Function

open scoped Bundle Manifold

section

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
    {H : Type*} [TopologicalSpace H] {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    {I : ModelWithCorners ğ•œ E H}
    {H' : Type*} [TopologicalSpace H'] {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E']
    {I' : ModelWithCorners ğ•œ E' H'}
    {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [SmoothManifoldWithCorners I M]
    {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M'] [SmoothManifoldWithCorners I' M']

variable (I I' M M') in
/-- The tangent bundle of a product is canonically isomorphic to the product of the tangent
bundles. -/
@[simps] def equivTangentBundleProd :
    TangentBundle (I.prod I') (M Ã— M') â‰ƒ (TangentBundle I M) Ã— (TangentBundle I' M') where
  toFun p := (âŸ¨p.1.1, p.2.1âŸ©, âŸ¨p.1.2, p.2.2âŸ©)
  invFun p := âŸ¨(p.1.1, p.2.1), (p.1.2, p.2.2)âŸ©
  left_inv _ := rfl
  right_inv _ := rfl

lemma equivTangentBundleProd_eq_tangentMap_prod_tangentMap :
    equivTangentBundleProd I I' M M' = fun (p : TangentBundle (I.prod I') (M Ã— M')) â†¦
      (tangentMap (I.prod I') I Prod.fst p, tangentMap (I.prod I') I' Prod.snd p) := by
  simp only [tangentMap_prod_fst, tangentMap_prod_snd]; rfl

lemma smooth_equivTangentBundleProd :
    Smooth (I.prod I').tangent (I.tangent.prod I'.tangent) (equivTangentBundleProd I I' M M') := by
  rw [equivTangentBundleProd_eq_tangentMap_prod_tangentMap]
  exact smooth_fst.tangentMap.prod_mk smooth_snd.tangentMap

open scoped Topology

/-- The canonical equivalence between the product of tangent bundles and the tangent bundle of a
product is smooth. -/
lemma smooth_equivTangentBundleProd_symm :
    Smooth (I.tangent.prod I'.tangent) (I.prod I').tangent
      (equivTangentBundleProd I I' M M').symm := by
  /- I am really unhappy with this proof, but I haven't found a more functorial one, so I have to
  unfold more definitions than I'd like. The argument goes as follows: since we're looking at a
  map into a vector bundle whose basis map is smooth, it suffices to check the smoothness of the
  second coordinate. We're in a product, so it suffices to check that each projection is smooth.
  We notice that the first projection is the composition of the tangent map to `(x : M) â†¦ (x, b.2)`
  with obvious extensions or restrictions, and the tangent map to a smooth map is smooth, so we're
  done.
  The issue is with checking differentiability everywhere (to justify that the derivative of a
  product is the product of the derivatives), and writing down things.
  -/
  simp only [Smooth, ContMDiff, Prod.forall]
  intro a b
  rw [contMDiffAt_totalSpace]
  simp only [equivTangentBundleProd, Equiv.coe_fn_symm_mk, TangentBundle.trivializationAt_apply,
    PartialHomeomorph.extend, prodChartedSpace_chartAt, PartialHomeomorph.prod_toPartialEquiv,
    PartialEquiv.prod, PartialHomeomorph.toFun_eq_coe, PartialHomeomorph.coe_coe_symm,
    modelWithCorners_prod_toPartialEquiv, ModelWithCorners.toPartialEquiv_coe,
    ModelWithCorners.toPartialEquiv_coe_symm, ModelWithCorners.source_eq, Set.univ_prod_univ,
    ModelWithCorners.target_eq, PartialEquiv.coe_trans, comp_def, PartialEquiv.coe_trans_symm,
    PartialEquiv.coe_symm_mk, modelWithCorners_prod_coe, comp_apply]
  refine âŸ¨?_, (contMDiffAt_prod_module_iff _).2 âŸ¨?_, ?_âŸ©âŸ©
  Â· exact ContMDiffAt.prod_map (smoothAt_proj (TangentSpace I)) (smoothAt_proj (TangentSpace I'))
  Â· -- check that, in the fiber, the first projection is smooth in charts
    -- for this, write down a map which is obviously smooth (`C` below) and then check that the two
    -- maps coincide, up to obvious compositions.
    have C : ContMDiffAt I.tangent (I.prod I').tangent âŠ¤ (fun (p : TangentBundle I M)
        â†¦ (âŸ¨(p.1, b.1), (p.2, 0)âŸ© : TangentBundle (I.prod I') (M Ã— M'))) a := by
      let f : M â†’ M Ã— M' := fun m â†¦ (m, b.1)
      have A : Smooth I.tangent (I.prod I').tangent (tangentMap I (I.prod I') f) := by
        apply Smooth.tangentMap
        exact smooth_id.prod_mk smooth_const
      have B : tangentMap I (I.prod I') f = fun p â†¦ âŸ¨(p.1, b.1), (p.2, 0)âŸ© := by
        ext p : 1
        exact tangentMap_prod_left I I'
      rw [B] at A
      exact A a
    have Z := ((contMDiffAt_totalSpace _ _).1 C).2
    simp only [modelWithCornersSelf_prod, TangentBundle.trivializationAt_apply,
      PartialHomeomorph.extend, prodChartedSpace_chartAt, PartialHomeomorph.prod_toPartialEquiv,
      PartialEquiv.prod, PartialHomeomorph.toFun_eq_coe, PartialHomeomorph.coe_coe_symm,
      modelWithCorners_prod_toPartialEquiv, ModelWithCorners.toPartialEquiv_coe,
      ModelWithCorners.toPartialEquiv_coe_symm, ModelWithCorners.source_eq, Set.univ_prod_univ,
      ModelWithCorners.target_eq, PartialEquiv.coe_trans, comp_def, PartialEquiv.coe_trans_symm,
      PartialEquiv.coe_symm_mk, modelWithCorners_prod_coe, comp_apply] at Z
    have D1 : ContMDiff (ğ“˜(ğ•œ, E Ã— E')) (ğ“˜(ğ•œ, E)) âŠ¤ (Prod.fst : E Ã— E' â†’ E) := by
      rw [contMDiff_iff_contDiff]; exact contDiff_fst
    have D2 : ContMDiffAt (I.tangent.prod I'.tangent) I.tangent âŠ¤ Prod.fst (a, b) := smoothAt_fst
    have U' := (ContMDiffAt.comp a D1.contMDiffAt Z).comp (a, b) D2
    apply U'.congr_of_eventuallyEq
    filter_upwards [chart_source_mem_nhds (ModelProd (ModelProd H E) (ModelProd H' E')) (a, b)]
      with p hp
    clear U' D1 D2 C Z
    simp only [prodChartedSpace_chartAt, PartialHomeomorph.prod_toPartialEquiv,
      PartialEquiv.prod_source, Set.mem_prod, TangentBundle.mem_chart_source_iff] at hp
    let Ï† (x : E) := I ((chartAt H a.proj) ((chartAt H p.1.proj).symm (I.symm x)))
    have D0 : DifferentiableWithinAt ğ•œ Ï† (Set.range I) (I ((chartAt H p.1.proj) p.1.proj)) := by
      apply ContDiffWithinAt.differentiableWithinAt (n := âŠ¤) _ le_top
      apply contDiffWithinAt_ext_coord_change
      simp [hp.1]
    have D (w : TangentBundle I' M') :
        DifferentiableWithinAt ğ•œ (Ï† âˆ˜ (Prod.fst : E Ã— E' â†’ E)) (Set.range (Prod.map â†‘I â†‘I'))
        (I ((chartAt H p.1.proj) p.1.proj), I' ((chartAt H' w.proj) w.proj)) :=
      DifferentiableWithinAt.comp (t := Set.range I) _ (by exact D0)
        differentiableWithinAt_fst (by simp [mapsTo_fst_prod])
    let Ï†' (x : E') := I' ((chartAt H' b.proj) ((chartAt H' b.proj).symm (I'.symm x)))
    have D0' : DifferentiableWithinAt ğ•œ Ï†' (Set.range I')
        (I' ((chartAt H' b.proj) b.proj)) := by
      apply ContDiffWithinAt.differentiableWithinAt (n := âŠ¤) _ le_top
      apply contDiffWithinAt_ext_coord_change
      simp
    have D' : DifferentiableWithinAt ğ•œ (Ï†' âˆ˜ Prod.snd) (Set.range (Prod.map I I'))
        (I ((chartAt H p.1.proj) p.1.proj), I' ((chartAt H' b.proj) b.proj)) :=
      DifferentiableWithinAt.comp (t := Set.range I') _ (by exact D0') differentiableWithinAt_snd
        (by simp [mapsTo_snd_prod])
    have U w : UniqueDiffWithinAt ğ•œ (Set.range (Prod.map I I'))
        (I ((chartAt H p.1.proj) p.1.proj), I' w) := by
      simp only [Set.range_prod_map]
      apply UniqueDiffWithinAt.prod
      Â· exact ModelWithCorners.uniqueDiffWithinAt_image I
      Â· exact ModelWithCorners.uniqueDiffWithinAt_image I'
    simp only [Set.range_prod_map, ContinuousLinearMap.prod_apply, comp_def, comp_apply]
    rw [DifferentiableWithinAt.fderivWithin_prod (by exact D _) ?_ (U _)]; swap
    Â· let Ï†'' (x : E') := I' ((chartAt H' b.proj) ((chartAt H' p.2.proj).symm (I'.symm x)))
      have D0' : DifferentiableWithinAt ğ•œ Ï†'' (Set.range I')
          (I' ((chartAt H' p.2.proj) p.2.proj)) := by
        apply ContDiffWithinAt.differentiableWithinAt (n := âŠ¤) _ le_top
        apply contDiffWithinAt_ext_coord_change
        simp [hp.2]
      have D' : DifferentiableWithinAt ğ•œ (Ï†'' âˆ˜ Prod.snd) (Set.range (Prod.map I I'))
          (I ((chartAt H p.1.proj) p.1.proj), I' ((chartAt H' p.2.proj) p.2.proj)) :=
        DifferentiableWithinAt.comp (t := Set.range I') _ (by exact D0')
          differentiableWithinAt_snd (by simp [mapsTo_snd_prod])
      exact D'
    rw [DifferentiableWithinAt.fderivWithin_prod (by exact D _) (by exact D') (U _)]
    change fderivWithin ğ•œ (Ï† âˆ˜ Prod.fst) _ _ _ = fderivWithin ğ•œ (Ï† âˆ˜ Prod.fst) _ _ _
    rw [Set.range_prod_map] at U âŠ¢
    rw [fderivWithin.comp _ (by exact D0) differentiableWithinAt_fst mapsTo_fst_prod (U _),
      fderivWithin.comp _ (by exact D0) differentiableWithinAt_fst mapsTo_fst_prod (U _)]
    simp [fderivWithin_fst, U]
  Â· -- check that, in the fiber, the second projection is smooth in charts
    -- for this, write down a map which is obviously smooth (`C` below) and then check that the two
    -- maps coincide, up to obvious compositions.
    have C : ContMDiffAt I'.tangent (I.prod I').tangent âŠ¤ (fun (p : TangentBundle I' M')
        â†¦ (âŸ¨(a.1, p.1), (0, p.2)âŸ© : TangentBundle (I.prod I') (M Ã— M'))) b := by
      let f : M' â†’ M Ã— M' := fun m â†¦ (a.1, m)
      have A : Smooth I'.tangent (I.prod I').tangent (tangentMap I' (I.prod I') f) := by
        apply Smooth.tangentMap
        exact smooth_const.prod_mk smooth_id
      have B : tangentMap I' (I.prod I') f = fun p â†¦ âŸ¨(a.1, p.1), (0, p.2)âŸ© := by
        ext p : 1
        exact tangentMap_prod_right I I'
      rw [B] at A
      exact A b
    have Z := ((contMDiffAt_totalSpace _ _).1 C).2
    simp only [modelWithCornersSelf_prod, TangentBundle.trivializationAt_apply,
      PartialHomeomorph.extend, prodChartedSpace_chartAt, PartialHomeomorph.prod_toPartialEquiv,
      PartialEquiv.prod, PartialHomeomorph.toFun_eq_coe, PartialHomeomorph.coe_coe_symm,
      modelWithCorners_prod_toPartialEquiv, ModelWithCorners.toPartialEquiv_coe,
      ModelWithCorners.toPartialEquiv_coe_symm, ModelWithCorners.source_eq, Set.univ_prod_univ,
      ModelWithCorners.target_eq, PartialEquiv.coe_trans, comp_def, PartialEquiv.coe_trans_symm,
      PartialEquiv.coe_symm_mk, modelWithCorners_prod_coe, comp_apply] at Z
    have D1 : ContMDiff (ğ“˜(ğ•œ, E Ã— E')) (ğ“˜(ğ•œ, E')) âŠ¤ (Prod.snd : E Ã— E' â†’ E') := by
      rw [contMDiff_iff_contDiff]; exact contDiff_snd
    have D2 : ContMDiffAt (I.tangent.prod I'.tangent) I'.tangent âŠ¤ Prod.snd (a, b) := smoothAt_snd
    have U' := (ContMDiffAt.comp b D1.contMDiffAt Z).comp (a, b) D2
    apply U'.congr_of_eventuallyEq
    filter_upwards [chart_source_mem_nhds (ModelProd (ModelProd H E) (ModelProd H' E')) (a, b)]
      with p hp
    clear U' D1 D2 C Z
    simp only [prodChartedSpace_chartAt, PartialHomeomorph.prod_toPartialEquiv,
      PartialEquiv.prod_source, Set.mem_prod, TangentBundle.mem_chart_source_iff] at hp
    let Ï† (x : E') := I' ((chartAt H' b.proj) ((chartAt H' p.2.proj).symm (I'.symm x)))
    have D0 : DifferentiableWithinAt ğ•œ Ï† (Set.range I') (I' ((chartAt H' p.2.proj) p.2.proj)) := by
      apply ContDiffWithinAt.differentiableWithinAt (n := âŠ¤) _ le_top
      apply contDiffWithinAt_ext_coord_change
      simp [hp.2]
    have D (w : TangentBundle I M) :
        DifferentiableWithinAt ğ•œ (Ï† âˆ˜ (Prod.snd : E Ã— E' â†’ E')) (Set.range (Prod.map â†‘I â†‘I'))
        (I ((chartAt H w.proj) w.proj), I' ((chartAt H' p.2.proj) p.2.proj)) :=
      DifferentiableWithinAt.comp (t := Set.range I') _ (by exact D0)
        differentiableWithinAt_snd (by simp [mapsTo_snd_prod])
    let Ï†' (x : E) := I ((chartAt H a.proj) ((chartAt H a.proj).symm (I.symm x)))
    have D0' : DifferentiableWithinAt ğ•œ Ï†' (Set.range I)
        (I ((chartAt H a.proj) a.proj)) := by
      apply ContDiffWithinAt.differentiableWithinAt (n := âŠ¤) _ le_top
      apply contDiffWithinAt_ext_coord_change
      simp
    have D' : DifferentiableWithinAt ğ•œ (Ï†' âˆ˜ Prod.fst) (Set.range (Prod.map I I'))
        (I ((chartAt H a.proj) a.proj), I' ((chartAt H' p.2.proj) p.2.proj)) :=
      DifferentiableWithinAt.comp (t := Set.range I) _ (by exact D0') differentiableWithinAt_fst
        (by simp [mapsTo_fst_prod])
    have U w : UniqueDiffWithinAt ğ•œ (Set.range (Prod.map I I'))
        (I w, I' ((chartAt H' p.2.proj) p.2.proj)) := by
      simp only [Set.range_prod_map]
      apply UniqueDiffWithinAt.prod
      Â· exact ModelWithCorners.uniqueDiffWithinAt_image I
      Â· exact ModelWithCorners.uniqueDiffWithinAt_image I'
    simp only [Set.range_prod_map, ContinuousLinearMap.prod_apply, comp_def, comp_apply]
    rw [DifferentiableWithinAt.fderivWithin_prod ?_ (by exact D _) (U _)]; swap
    Â· let Ï†'' (x : E) := I ((chartAt H a.proj) ((chartAt H p.1.proj).symm (I.symm x)))
      have D0' : DifferentiableWithinAt ğ•œ Ï†'' (Set.range I)
          (I ((chartAt H p.1.proj) p.1.proj)) := by
        apply ContDiffWithinAt.differentiableWithinAt (n := âŠ¤) _ le_top
        apply contDiffWithinAt_ext_coord_change
        simp [hp.1]
      have D' : DifferentiableWithinAt ğ•œ (Ï†'' âˆ˜ Prod.fst) (Set.range (Prod.map I I'))
          (I ((chartAt H p.1.proj) p.1.proj), I' ((chartAt H' p.2.proj) p.2.proj)) :=
        DifferentiableWithinAt.comp (t := Set.range I) _ (by exact D0')
          differentiableWithinAt_fst (by simp [mapsTo_fst_prod])
      exact D'
    rw [DifferentiableWithinAt.fderivWithin_prod (by exact D') (by exact D a) (U _)]
    change fderivWithin ğ•œ (Ï† âˆ˜ Prod.snd) _ _ _ = fderivWithin ğ•œ (Ï† âˆ˜ Prod.snd) _ _ _
    rw [Set.range_prod_map] at U âŠ¢
    rw [fderivWithin.comp _ (by exact D0) differentiableWithinAt_snd mapsTo_snd_prod (U _),
      fderivWithin.comp _ (by exact D0) differentiableWithinAt_snd mapsTo_snd_prod (U _)]
    simp [fderivWithin_snd, U]







variable {G : Type*} [TopologicalSpace G] [ChartedSpace H G] [Group G] [LieGroup I G]

/-- The invariant vector field associated to a vector in the Lie alebra. -/
def invariantVectorField (v : TangentSpace I (1 : G)) (g : G) : TangentSpace I g :=
  mfderiv I I (fun a â†¦ g * a) (1 : G) v

theorem contMDiff_invariantVectorField (v : TangentSpace I (1 : G)) :
    ContMDiff I I.tangent âŠ¤
      (fun (g : G) â†¦ (invariantVectorField v g : TangentBundle I G)) := by
  let fg : G â†’ TangentBundle I G := fun g â†¦ TotalSpace.mk' E g 0
  have sfg : Smooth I I.tangent fg := smooth_zeroSection _ _
  let fv : G â†’ TangentBundle I G := fun _ â†¦ TotalSpace.mk' E 1 v
  have sfv : Smooth I I.tangent fv := smooth_const
  let Fâ‚ : G â†’ (TangentBundle I G Ã— TangentBundle I G) := fun g â†¦ (fg g, fv g)
  have Sâ‚ : Smooth I (I.tangent.prod I.tangent) Fâ‚ := Smooth.prod_mk sfg sfv
  let Fâ‚‚ : (TangentBundle I G Ã— TangentBundle I G) â†’ TangentBundle (I.prod I) (G Ã— G) :=
    (equivTangentBundleProd I I G G).symm
  have Sâ‚‚ : Smooth (I.tangent.prod I.tangent) (I.prod I).tangent Fâ‚‚ :=
    smooth_equivTangentBundleProd_symm
  let Fâ‚ƒ : TangentBundle (I.prod I) (G Ã— G) â†’ TangentBundle I G :=
    tangentMap (I.prod I) I (fun (p : G Ã— G) â†¦ p.1 * p.2)
  have Sâ‚ƒ : Smooth (I.prod I).tangent I.tangent Fâ‚ƒ := by
    apply ContMDiff.contMDiff_tangentMap _ (m := âŠ¤) le_rfl
    exact smooth_mul I (G := G)
  let S := (Sâ‚ƒ.comp Sâ‚‚).comp Sâ‚
  convert S with g
  Â· simp [Fâ‚, Fâ‚‚, Fâ‚ƒ]
  Â· simp only [comp_apply, tangentMap, Fâ‚ƒ, Fâ‚‚, Fâ‚]
    rw [mfderiv_prod_eq_add_apply _ _ _ (smooth_mul I (G := G)).mdifferentiableAt]
    simp [invariantVectorField]

end
